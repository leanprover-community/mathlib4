/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro
-/
import Mathlib.Topology.Compactness.SigmaCompact
import Mathlib.Topology.Connected.LocallyConnected
import Mathlib.Topology.Connected.TotallyDisconnected
import Mathlib.Topology.Inseparable

#align_import topology.separation from "leanprover-community/mathlib"@"d91e7f7a7f1c7e9f0e18fdb6bde4f652004c735d"

/-!
# Separation properties of topological spaces.

This file defines the predicate `SeparatedNhds`, and common separation axioms
(under the Kolmogorov classification).

## Main definitions

* `SeparatedNhds`: Two `Set`s are separated by neighbourhoods if they are contained in disjoint
  open sets.
* `T0Space`: A T‚ÇÄ/Kolmogorov space is a space where, for every two points `x ‚â† y`,
  there is an open set that contains one, but not the other.
* `T1Space`: A T‚ÇÅ/Fr√©chet space is a space where every singleton set is closed.
  This is equivalent to, for every pair `x ‚â† y`, there existing an open set containing `x`
  but not `y` (`t1Space_iff_exists_open` shows that these conditions are equivalent.)
* `T2Space`: A T‚ÇÇ/Hausdorff space is a space where, for every two points `x ‚â† y`,
  there is two disjoint open sets, one containing `x`, and the other `y`.
* `T25Space`: A T‚ÇÇ.‚ÇÖ/Urysohn space is a space where, for every two points `x ‚â† y`,
  there is two open sets, one containing `x`, and the other `y`, whose closures are disjoint.
* `T3Space`: A T‚ÇÉ space, is one where given any closed `C` and `x ‚àâ C`,
  there is disjoint open sets containing `x` and `C` respectively. In `mathlib`, T‚ÇÉ implies T‚ÇÇ.‚ÇÖ.
* `NormalSpace`: A normal space, is one where given two disjoint closed sets,
  we can find two open sets that separate them.
* `T4Space`: A T‚ÇÑ space is a normal T‚ÇÅ space. T‚ÇÑ implies T‚ÇÉ.
* `T5Space`: A T‚ÇÖ space, also known as a *completely normal Hausdorff space*

## Main results

### T‚ÇÄ spaces

* `IsClosed.exists_closed_singleton` Given a closed set `S` in a compact T‚ÇÄ space,
  there is some `x ‚àà S` such that `{x}` is closed.
* `exists_open_singleton_of_open_finset` Given an open `Finset` `S` in a T‚ÇÄ space,
  there is some `x ‚àà S` such that `{x}` is open.

### T‚ÇÅ spaces

* `isClosedMap_const`: The constant map is a closed map.
* `discrete_of_t1_of_finite`: A finite T‚ÇÅ space must have the discrete topology.

### T‚ÇÇ spaces

* `t2_iff_nhds`: A space is T‚ÇÇ iff the neighbourhoods of distinct points generate the bottom filter.
* `t2_iff_isClosed_diagonal`: A space is T‚ÇÇ iff the `diagonal` of `Œ±` (that is, the set of all
  points of the form `(a, a) : Œ± √ó Œ±`) is closed under the product topology.
* `finset_disjoint_finset_opens_of_t2`: Any two disjoint finsets are `SeparatedNhds`.
* Most topological constructions preserve Hausdorffness;
  these results are part of the typeclass inference system (e.g. `Embedding.t2Space`)
* `Set.EqOn.closure`: If two functions are equal on some set `s`, they are equal on its closure.
* `IsCompact.isClosed`: All compact sets are closed.
* `WeaklyLocallyCompactSpace.locallyCompactSpace`: If a topological space is both
  weakly locally compact (i.e., each point has a compact neighbourhood)
  and is T‚ÇÇ, then it is locally compact.
* `totallySeparatedSpace_of_t1_of_basis_clopen`: If `Œ±` has a clopen basis, then
  it is a `TotallySeparatedSpace`.
* `loc_compact_t2_tot_disc_iff_tot_sep`: A locally compact T‚ÇÇ space is totally disconnected iff
  it is totally separated.

If the space is also compact:

* `normalOfCompactT2`: A compact T‚ÇÇ space is a `NormalSpace`.
* `connectedComponent_eq_iInter_clopen`: The connected component of a point
  is the intersection of all its clopen neighbourhoods.
* `compact_t2_tot_disc_iff_tot_sep`: Being a `TotallyDisconnectedSpace`
  is equivalent to being a `TotallySeparatedSpace`.
* `ConnectedComponents.t2`: `ConnectedComponents Œ±` is T‚ÇÇ for `Œ±` T‚ÇÇ and compact.

### T‚ÇÉ spaces

* `disjoint_nested_nhds`: Given two points `x ‚â† y`, we can find neighbourhoods `x ‚àà V‚ÇÅ ‚äÜ U‚ÇÅ` and
  `y ‚àà V‚ÇÇ ‚äÜ U‚ÇÇ`, with the `V‚Çñ` closed and the `U‚Çñ` open, such that the `U‚Çñ` are disjoint.

## References

https://en.wikipedia.org/wiki/Separation_axiom
-/


open Function Set Filter Topology TopologicalSpace
open scoped Classical

universe u v

variable {Œ± : Type u} {Œ≤ : Type v} [TopologicalSpace Œ±]

section Separation

/--
`SeparatedNhds` is a predicate on pairs of sub`Set`s of a topological space.  It holds if the two
sub`Set`s are contained in disjoint open sets.
-/
def SeparatedNhds : Set Œ± ‚Üí Set Œ± ‚Üí Prop := fun s t : Set Œ± =>
  ‚àÉ U V : Set Œ±, IsOpen U ‚àß IsOpen V ‚àß s ‚äÜ U ‚àß t ‚äÜ V ‚àß Disjoint U V
#align separated_nhds SeparatedNhds

theorem separatedNhds_iff_disjoint {s t : Set Œ±} : SeparatedNhds s t ‚Üî Disjoint (ùìùÀ¢ s) (ùìùÀ¢ t) := by
  simp only [(hasBasis_nhdsSet s).disjoint_iff (hasBasis_nhdsSet t), SeparatedNhds, exists_prop, ‚Üê
    exists_and_left, and_assoc, and_comm, and_left_comm]
#align separated_nhds_iff_disjoint separatedNhds_iff_disjoint

alias ‚ü®SeparatedNhds.disjoint_nhdsSet, _‚ü© := separatedNhds_iff_disjoint

namespace SeparatedNhds

variable {s s‚ÇÅ s‚ÇÇ t t‚ÇÅ t‚ÇÇ u : Set Œ±}

@[symm]
theorem symm : SeparatedNhds s t ‚Üí SeparatedNhds t s := fun ‚ü®U, V, oU, oV, aU, bV, UV‚ü© =>
  ‚ü®V, U, oV, oU, bV, aU, Disjoint.symm UV‚ü©
#align separated_nhds.symm SeparatedNhds.symm

theorem comm (s t : Set Œ±) : SeparatedNhds s t ‚Üî SeparatedNhds t s :=
  ‚ü®symm, symm‚ü©
#align separated_nhds.comm SeparatedNhds.comm

theorem preimage [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤} {s t : Set Œ≤} (h : SeparatedNhds s t)
    (hf : Continuous f) : SeparatedNhds (f ‚Åª¬π' s) (f ‚Åª¬π' t) :=
  let ‚ü®U, V, oU, oV, sU, tV, UV‚ü© := h
  ‚ü®f ‚Åª¬π' U, f ‚Åª¬π' V, oU.preimage hf, oV.preimage hf, preimage_mono sU, preimage_mono tV,
    UV.preimage f‚ü©
#align separated_nhds.preimage SeparatedNhds.preimage

protected theorem disjoint (h : SeparatedNhds s t) : Disjoint s t :=
  let ‚ü®_, _, _, _, hsU, htV, hd‚ü© := h; hd.mono hsU htV
#align separated_nhds.disjoint SeparatedNhds.disjoint

theorem disjoint_closure_left (h : SeparatedNhds s t) : Disjoint (closure s) t :=
  let ‚ü®_U, _V, _, hV, hsU, htV, hd‚ü© := h
  (hd.closure_left hV).mono (closure_mono hsU) htV
#align separated_nhds.disjoint_closure_left SeparatedNhds.disjoint_closure_left

theorem disjoint_closure_right (h : SeparatedNhds s t) : Disjoint s (closure t) :=
  h.symm.disjoint_closure_left.symm
#align separated_nhds.disjoint_closure_right SeparatedNhds.disjoint_closure_right

theorem empty_right (s : Set Œ±) : SeparatedNhds s ‚àÖ :=
  ‚ü®_, _, isOpen_univ, isOpen_empty, fun a _ => mem_univ a, Subset.rfl, disjoint_empty _‚ü©
#align separated_nhds.empty_right SeparatedNhds.empty_right

theorem empty_left (s : Set Œ±) : SeparatedNhds ‚àÖ s :=
  (empty_right _).symm
#align separated_nhds.empty_left SeparatedNhds.empty_left

theorem mono (h : SeparatedNhds s‚ÇÇ t‚ÇÇ) (hs : s‚ÇÅ ‚äÜ s‚ÇÇ) (ht : t‚ÇÅ ‚äÜ t‚ÇÇ) : SeparatedNhds s‚ÇÅ t‚ÇÅ :=
  let ‚ü®U, V, hU, hV, hsU, htV, hd‚ü© := h
  ‚ü®U, V, hU, hV, hs.trans hsU, ht.trans htV, hd‚ü©
#align separated_nhds.mono SeparatedNhds.mono

theorem union_left : SeparatedNhds s u ‚Üí SeparatedNhds t u ‚Üí SeparatedNhds (s ‚à™ t) u := by
  simpa only [separatedNhds_iff_disjoint, nhdsSet_union, disjoint_sup_left] using And.intro
#align separated_nhds.union_left SeparatedNhds.union_left

theorem union_right (ht : SeparatedNhds s t) (hu : SeparatedNhds s u) : SeparatedNhds s (t ‚à™ u) :=
  (ht.symm.union_left hu.symm).symm
#align separated_nhds.union_right SeparatedNhds.union_right

end SeparatedNhds

/-- A T‚ÇÄ space, also known as a Kolmogorov space, is a topological space such that for every pair
`x ‚â† y`, there is an open set containing one but not the other. We formulate the definition in terms
of the `Inseparable` relation.  -/
class T0Space (Œ± : Type u) [TopologicalSpace Œ±] : Prop where
  /-- Two inseparable points in a T‚ÇÄ space are equal. -/
  t0 : ‚àÄ ‚¶Éx y : Œ±‚¶Ñ, Inseparable x y ‚Üí x = y
#align t0_space T0Space

theorem t0Space_iff_inseparable (Œ± : Type u) [TopologicalSpace Œ±] :
    T0Space Œ± ‚Üî ‚àÄ x y : Œ±, Inseparable x y ‚Üí x = y :=
  ‚ü®fun ‚ü®h‚ü© => h, fun h => ‚ü®h‚ü©‚ü©
#align t0_space_iff_inseparable t0Space_iff_inseparable

theorem t0Space_iff_not_inseparable (Œ± : Type u) [TopologicalSpace Œ±] :
    T0Space Œ± ‚Üî ‚àÄ x y : Œ±, x ‚â† y ‚Üí ¬¨Inseparable x y := by
  simp only [t0Space_iff_inseparable, Ne.def, not_imp_not]
#align t0_space_iff_not_inseparable t0Space_iff_not_inseparable

theorem Inseparable.eq [T0Space Œ±] {x y : Œ±} (h : Inseparable x y) : x = y :=
  T0Space.t0 h
#align inseparable.eq Inseparable.eq

/-- A topology `Inducing` map from a T‚ÇÄ space is injective. -/
protected theorem Inducing.injective [TopologicalSpace Œ≤] [T0Space Œ±] {f : Œ± ‚Üí Œ≤}
    (hf : Inducing f) : Injective f := fun _ _ h =>
  (hf.inseparable_iff.1 <| .of_eq h).eq
#align inducing.injective Inducing.injective

/-- A topology `Inducing` map from a T‚ÇÄ space is a topological embedding. -/
protected theorem Inducing.embedding [TopologicalSpace Œ≤] [T0Space Œ±] {f : Œ± ‚Üí Œ≤}
    (hf : Inducing f) : Embedding f :=
  ‚ü®hf, hf.injective‚ü©
#align inducing.embedding Inducing.embedding

lemma embedding_iff_inducing [TopologicalSpace Œ≤] [T0Space Œ±] {f : Œ± ‚Üí Œ≤} :
    Embedding f ‚Üî Inducing f :=
  ‚ü®Embedding.toInducing, Inducing.embedding‚ü©
#align embedding_iff_inducing embedding_iff_inducing

theorem t0Space_iff_nhds_injective (Œ± : Type u) [TopologicalSpace Œ±] :
    T0Space Œ± ‚Üî Injective (ùìù : Œ± ‚Üí Filter Œ±) :=
  t0Space_iff_inseparable Œ±
#align t0_space_iff_nhds_injective t0Space_iff_nhds_injective

theorem nhds_injective [T0Space Œ±] : Injective (ùìù : Œ± ‚Üí Filter Œ±) :=
  (t0Space_iff_nhds_injective Œ±).1 ‚Äπ_‚Ä∫
#align nhds_injective nhds_injective

theorem inseparable_iff_eq [T0Space Œ±] {x y : Œ±} : Inseparable x y ‚Üî x = y :=
  nhds_injective.eq_iff
#align inseparable_iff_eq inseparable_iff_eq

@[simp]
theorem nhds_eq_nhds_iff [T0Space Œ±] {a b : Œ±} : ùìù a = ùìù b ‚Üî a = b :=
  nhds_injective.eq_iff
#align nhds_eq_nhds_iff nhds_eq_nhds_iff

@[simp]
theorem inseparable_eq_eq [T0Space Œ±] : Inseparable = @Eq Œ± :=
  funext‚ÇÇ fun _ _ => propext inseparable_iff_eq
#align inseparable_eq_eq inseparable_eq_eq

theorem TopologicalSpace.IsTopologicalBasis.inseparable_iff {b : Set (Set Œ±)}
    (hb : IsTopologicalBasis b) {x y : Œ±} : Inseparable x y ‚Üî ‚àÄ s ‚àà b, (x ‚àà s ‚Üî y ‚àà s) :=
  ‚ü®fun h s hs ‚Ü¶ inseparable_iff_forall_open.1 h _ (hb.isOpen hs),
    fun h ‚Ü¶ hb.nhds_hasBasis.eq_of_same_basis $ by
      convert hb.nhds_hasBasis using 2
      exact and_congr_right (h _)‚ü©

theorem TopologicalSpace.IsTopologicalBasis.eq_iff [T0Space Œ±] {b : Set (Set Œ±)}
    (hb : IsTopologicalBasis b) {x y : Œ±} : x = y ‚Üî ‚àÄ s ‚àà b, (x ‚àà s ‚Üî y ‚àà s) :=
  inseparable_iff_eq.symm.trans hb.inseparable_iff

theorem t0Space_iff_exists_isOpen_xor'_mem (Œ± : Type u) [TopologicalSpace Œ±] :
    T0Space Œ± ‚Üî ‚àÄ x y, x ‚â† y ‚Üí ‚àÉ U : Set Œ±, IsOpen U ‚àß Xor' (x ‚àà U) (y ‚àà U) := by
  simp only [t0Space_iff_not_inseparable, xor_iff_not_iff, not_forall, exists_prop,
    inseparable_iff_forall_open]
#align t0_space_iff_exists_is_open_xor_mem t0Space_iff_exists_isOpen_xor'_mem

theorem exists_isOpen_xor'_mem [T0Space Œ±] {x y : Œ±} (h : x ‚â† y) :
    ‚àÉ U : Set Œ±, IsOpen U ‚àß Xor' (x ‚àà U) (y ‚àà U) :=
  (t0Space_iff_exists_isOpen_xor'_mem Œ±).1 ‚Äπ_‚Ä∫ x y h
#align exists_is_open_xor_mem exists_isOpen_xor'_mem

/-- Specialization forms a partial order on a t0 topological space. -/
def specializationOrder (Œ± : Type*) [TopologicalSpace Œ±] [T0Space Œ±] : PartialOrder Œ± :=
  { specializationPreorder Œ±, PartialOrder.lift (OrderDual.toDual ‚àò ùìù) nhds_injective with }
#align specialization_order specializationOrder

instance : T0Space (SeparationQuotient Œ±) :=
  ‚ü®fun x y => Quotient.inductionOn‚ÇÇ' x y fun _ _ h =>
    SeparationQuotient.mk_eq_mk.2 <| SeparationQuotient.inducing_mk.inseparable_iff.1 h‚ü©

theorem minimal_nonempty_closed_subsingleton [T0Space Œ±] {s : Set Œ±} (hs : IsClosed s)
    (hmin : ‚àÄ t, t ‚äÜ s ‚Üí t.Nonempty ‚Üí IsClosed t ‚Üí t = s) : s.Subsingleton := by
  clear Œ≤ -- Porting note: added
  refine' fun x hx y hy => of_not_not fun hxy => _
  rcases exists_isOpen_xor'_mem hxy with ‚ü®U, hUo, hU‚ü©
  wlog h : x ‚àà U ‚àß y ‚àâ U
  ¬∑ refine this hs hmin y hy x hx (Ne.symm hxy) U hUo hU.symm (hU.resolve_left h)
  cases' h with hxU hyU
  have : s \ U = s := hmin (s \ U) (diff_subset _ _) ‚ü®y, hy, hyU‚ü© (hs.sdiff hUo)
  exact (this.symm.subset hx).2 hxU
#align minimal_nonempty_closed_subsingleton minimal_nonempty_closed_subsingleton

theorem minimal_nonempty_closed_eq_singleton [T0Space Œ±] {s : Set Œ±} (hs : IsClosed s)
    (hne : s.Nonempty) (hmin : ‚àÄ t, t ‚äÜ s ‚Üí t.Nonempty ‚Üí IsClosed t ‚Üí t = s) : ‚àÉ x, s = {x} :=
  exists_eq_singleton_iff_nonempty_subsingleton.2
    ‚ü®hne, minimal_nonempty_closed_subsingleton hs hmin‚ü©
#align minimal_nonempty_closed_eq_singleton minimal_nonempty_closed_eq_singleton

/-- Given a closed set `S` in a compact T‚ÇÄ space, there is some `x ‚àà S` such that `{x}` is
closed. -/
theorem IsClosed.exists_closed_singleton {Œ± : Type*} [TopologicalSpace Œ±] [T0Space Œ±]
    [CompactSpace Œ±] {S : Set Œ±} (hS : IsClosed S) (hne : S.Nonempty) :
    ‚àÉ x : Œ±, x ‚àà S ‚àß IsClosed ({x} : Set Œ±) := by
  obtain ‚ü®V, Vsub, Vne, Vcls, hV‚ü© := hS.exists_minimal_nonempty_closed_subset hne
  rcases minimal_nonempty_closed_eq_singleton Vcls Vne hV with ‚ü®x, rfl‚ü©
  exact ‚ü®x, Vsub (mem_singleton x), Vcls‚ü©
#align is_closed.exists_closed_singleton IsClosed.exists_closed_singleton

theorem minimal_nonempty_open_subsingleton [T0Space Œ±] {s : Set Œ±} (hs : IsOpen s)
    (hmin : ‚àÄ t, t ‚äÜ s ‚Üí t.Nonempty ‚Üí IsOpen t ‚Üí t = s) : s.Subsingleton := by
  clear Œ≤ -- Porting note: added
  refine' fun x hx y hy => of_not_not fun hxy => _
  rcases exists_isOpen_xor'_mem hxy with ‚ü®U, hUo, hU‚ü©
  wlog h : x ‚àà U ‚àß y ‚àâ U
  ¬∑ exact this hs hmin y hy x hx (Ne.symm hxy) U hUo hU.symm (hU.resolve_left h)
  cases' h with hxU hyU
  have : s ‚à© U = s := hmin (s ‚à© U) (inter_subset_left _ _) ‚ü®x, hx, hxU‚ü© (hs.inter hUo)
  exact hyU (this.symm.subset hy).2
#align minimal_nonempty_open_subsingleton minimal_nonempty_open_subsingleton

theorem minimal_nonempty_open_eq_singleton [T0Space Œ±] {s : Set Œ±} (hs : IsOpen s)
    (hne : s.Nonempty) (hmin : ‚àÄ t, t ‚äÜ s ‚Üí t.Nonempty ‚Üí IsOpen t ‚Üí t = s) : ‚àÉ x, s = {x} :=
  exists_eq_singleton_iff_nonempty_subsingleton.2 ‚ü®hne, minimal_nonempty_open_subsingleton hs hmin‚ü©
#align minimal_nonempty_open_eq_singleton minimal_nonempty_open_eq_singleton

/-- Given an open finite set `S` in a T‚ÇÄ space, there is some `x ‚àà S` such that `{x}` is open. -/
theorem exists_open_singleton_of_open_finite [T0Space Œ±] {s : Set Œ±} (hfin : s.Finite)
    (hne : s.Nonempty) (ho : IsOpen s) : ‚àÉ x ‚àà s, IsOpen ({x} : Set Œ±) := by
  lift s to Finset Œ± using hfin
  induction' s using Finset.strongInductionOn with s ihs
  rcases em (‚àÉ t, t ‚äÇ s ‚àß t.Nonempty ‚àß IsOpen (t : Set Œ±)) with (‚ü®t, hts, htne, hto‚ü© | ht)
  ¬∑ rcases ihs t hts htne hto with ‚ü®x, hxt, hxo‚ü©
    exact ‚ü®x, hts.1 hxt, hxo‚ü©
  ¬∑ -- Porting note: was `rcases minimal_nonempty_open_eq_singleton ho hne _ with ‚ü®x, hx‚ü©`
    --               https://github.com/leanprover/std4/issues/116
    rsuffices ‚ü®x, hx‚ü© : ‚àÉ x, s.toSet = {x}
    ¬∑ exact ‚ü®x, hx.symm ‚ñ∏ rfl, hx ‚ñ∏ ho‚ü©
    refine minimal_nonempty_open_eq_singleton ho hne ?_
    refine' fun t hts htne hto => of_not_not fun hts' => ht _
    lift t to Finset Œ± using s.finite_toSet.subset hts
    exact ‚ü®t, ssubset_iff_subset_ne.2 ‚ü®hts, mt Finset.coe_inj.2 hts'‚ü©, htne, hto‚ü©
#align exists_open_singleton_of_open_finite exists_open_singleton_of_open_finite

theorem exists_open_singleton_of_finite [T0Space Œ±] [Finite Œ±] [Nonempty Œ±] :
    ‚àÉ x : Œ±, IsOpen ({x} : Set Œ±) :=
  let ‚ü®x, _, h‚ü© := exists_open_singleton_of_open_finite (Set.toFinite _) univ_nonempty isOpen_univ
  ‚ü®x, h‚ü©
#align exists_open_singleton_of_fintype exists_open_singleton_of_finite

theorem t0Space_of_injective_of_continuous [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : Function.Injective f) (hf' : Continuous f) [T0Space Œ≤] : T0Space Œ± :=
  ‚ü®fun _ _ h => hf <| (h.map hf').eq‚ü©
#align t0_space_of_injective_of_continuous t0Space_of_injective_of_continuous

protected theorem Embedding.t0Space [TopologicalSpace Œ≤] [T0Space Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : Embedding f) : T0Space Œ± :=
  t0Space_of_injective_of_continuous hf.inj hf.continuous
#align embedding.t0_space Embedding.t0Space

instance Subtype.t0Space [T0Space Œ±] {p : Œ± ‚Üí Prop} : T0Space (Subtype p) :=
  embedding_subtype_val.t0Space
#align subtype.t0_space Subtype.t0Space

theorem t0Space_iff_or_not_mem_closure (Œ± : Type u) [TopologicalSpace Œ±] :
    T0Space Œ± ‚Üî ‚àÄ a b : Œ±, a ‚â† b ‚Üí a ‚àâ closure ({b} : Set Œ±) ‚à® b ‚àâ closure ({a} : Set Œ±) := by
  simp only [t0Space_iff_not_inseparable, inseparable_iff_mem_closure, not_and_or]
#align t0_space_iff_or_not_mem_closure t0Space_iff_or_not_mem_closure

instance Prod.instT0Space [TopologicalSpace Œ≤] [T0Space Œ±] [T0Space Œ≤] : T0Space (Œ± √ó Œ≤) :=
  ‚ü®fun _ _ h => Prod.ext (h.map continuous_fst).eq (h.map continuous_snd).eq‚ü©

instance Pi.instT0Space {Œπ : Type*} {œÄ : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (œÄ i)]
    [‚àÄ i, T0Space (œÄ i)] :
    T0Space (‚àÄ i, œÄ i) :=
  ‚ü®fun _ _ h => funext fun i => (h.map (continuous_apply i)).eq‚ü©
#align pi.t0_space Pi.instT0Space

theorem T0Space.of_cover (h : ‚àÄ x y, Inseparable x y ‚Üí ‚àÉ s : Set Œ±, x ‚àà s ‚àß y ‚àà s ‚àß T0Space s) :
    T0Space Œ± := by
  refine' ‚ü®fun x y hxy => _‚ü©
  rcases h x y hxy with ‚ü®s, hxs, hys, hs‚ü©
  lift x to s using hxs; lift y to s using hys
  rw [‚Üê subtype_inseparable_iff] at hxy
  exact congr_arg Subtype.val hxy.eq
#align t0_space.of_cover T0Space.of_cover

theorem T0Space.of_open_cover (h : ‚àÄ x, ‚àÉ s : Set Œ±, x ‚àà s ‚àß IsOpen s ‚àß T0Space s) : T0Space Œ± :=
  T0Space.of_cover fun x _ hxy =>
    let ‚ü®s, hxs, hso, hs‚ü© := h x
    ‚ü®s, hxs, (hxy.mem_open_iff hso).1 hxs, hs‚ü©
#align t0_space.of_open_cover T0Space.of_open_cover

/-- A T‚ÇÅ space, also known as a Fr√©chet space, is a topological space
  where every singleton set is closed. Equivalently, for every pair
  `x ‚â† y`, there is an open set containing `x` and not `y`. -/
class T1Space (Œ± : Type u) [TopologicalSpace Œ±] : Prop where
  /-- A singleton in a T‚ÇÅ space is a closed set. -/
  t1 : ‚àÄ x, IsClosed ({x} : Set Œ±)
#align t1_space T1Space

theorem isClosed_singleton [T1Space Œ±] {x : Œ±} : IsClosed ({x} : Set Œ±) :=
  T1Space.t1 x
#align is_closed_singleton isClosed_singleton

theorem isOpen_compl_singleton [T1Space Œ±] {x : Œ±} : IsOpen ({x}·∂ú : Set Œ±) :=
  isClosed_singleton.isOpen_compl
#align is_open_compl_singleton isOpen_compl_singleton

theorem isOpen_ne [T1Space Œ±] {x : Œ±} : IsOpen { y | y ‚â† x } :=
  isOpen_compl_singleton
#align is_open_ne isOpen_ne

@[to_additive]
theorem Continuous.isOpen_mulSupport [T1Space Œ±] [One Œ±] [TopologicalSpace Œ≤] {f : Œ≤ ‚Üí Œ±}
    (hf : Continuous f) : IsOpen (mulSupport f) :=
  isOpen_ne.preimage hf
#align continuous.is_open_mul_support Continuous.isOpen_mulSupport
#align continuous.is_open_support Continuous.isOpen_support

theorem Ne.nhdsWithin_compl_singleton [T1Space Œ±] {x y : Œ±} (h : x ‚â† y) : ùìù[{y}·∂ú] x = ùìù x :=
  isOpen_ne.nhdsWithin_eq h
#align ne.nhds_within_compl_singleton Ne.nhdsWithin_compl_singleton

theorem Ne.nhdsWithin_diff_singleton [T1Space Œ±] {x y : Œ±} (h : x ‚â† y) (s : Set Œ±) :
    ùìù[s \ {y}] x = ùìù[s] x := by
  rw [diff_eq, inter_comm, nhdsWithin_inter_of_mem]
  exact mem_nhdsWithin_of_mem_nhds (isOpen_ne.mem_nhds h)
#align ne.nhds_within_diff_singleton Ne.nhdsWithin_diff_singleton

lemma nhdsWithin_compl_singleton_le [T1Space Œ±] (x y : Œ±) : ùìù[{x}·∂ú] x ‚â§ ùìù[{y}·∂ú] x := by
  rcases eq_or_ne x y with rfl|hy
  ¬∑ exact Eq.le rfl
  ¬∑ rw [Ne.nhdsWithin_compl_singleton hy]
    exact nhdsWithin_le_nhds

theorem isOpen_setOf_eventually_nhdsWithin [T1Space Œ±] {p : Œ± ‚Üí Prop} :
    IsOpen { x | ‚àÄ·∂† y in ùìù[‚â†] x, p y } := by
  refine' isOpen_iff_mem_nhds.mpr fun a ha => _
  filter_upwards [eventually_nhds_nhdsWithin.mpr ha] with b hb
  rcases eq_or_ne a b with rfl | h
  ¬∑ exact hb
  ¬∑ rw [h.symm.nhdsWithin_compl_singleton] at hb
    exact hb.filter_mono nhdsWithin_le_nhds
#align is_open_set_of_eventually_nhds_within isOpen_setOf_eventually_nhdsWithin

protected theorem Set.Finite.isClosed [T1Space Œ±] {s : Set Œ±} (hs : Set.Finite s) : IsClosed s := by
  rw [‚Üê biUnion_of_singleton s]
  exact hs.isClosed_biUnion fun i _ => isClosed_singleton
#align set.finite.is_closed Set.Finite.isClosed

theorem TopologicalSpace.IsTopologicalBasis.exists_mem_of_ne [T1Space Œ±] {b : Set (Set Œ±)}
    (hb : IsTopologicalBasis b) {x y : Œ±} (h : x ‚â† y) : ‚àÉ a ‚àà b, x ‚àà a ‚àß y ‚àâ a := by
  rcases hb.isOpen_iff.1 isOpen_ne x h with ‚ü®a, ab, xa, ha‚ü©
  exact ‚ü®a, ab, xa, fun h => ha h rfl‚ü©
#align topological_space.is_topological_basis.exists_mem_of_ne TopologicalSpace.IsTopologicalBasis.exists_mem_of_ne

theorem Filter.coclosedCompact_le_cofinite [T1Space Œ±] :
    Filter.coclosedCompact Œ± ‚â§ Filter.cofinite := fun s hs =>
  compl_compl s ‚ñ∏ hs.isCompact.compl_mem_coclosedCompact_of_isClosed hs.isClosed
#align filter.coclosed_compact_le_cofinite Filter.coclosedCompact_le_cofinite

variable (Œ±)

/-- In a `T1Space`, relatively compact sets form a bornology. Its cobounded filter is
`Filter.coclosedCompact`. See also `Bornology.inCompact` the bornology of sets contained
in a compact set. -/
def Bornology.relativelyCompact [T1Space Œ±] : Bornology Œ± where
  cobounded' := Filter.coclosedCompact Œ±
  le_cofinite' := Filter.coclosedCompact_le_cofinite
#align bornology.relatively_compact Bornology.relativelyCompact

variable {Œ±}

theorem Bornology.relativelyCompact.isBounded_iff [T1Space Œ±] {s : Set Œ±} :
    @Bornology.IsBounded _ (Bornology.relativelyCompact Œ±) s ‚Üî IsCompact (closure s) := by
  change s·∂ú ‚àà Filter.coclosedCompact Œ± ‚Üî _
  rw [Filter.mem_coclosedCompact]
  constructor
  ¬∑ rintro ‚ü®t, ht‚ÇÅ, ht‚ÇÇ, hst‚ü©
    rw [compl_subset_compl] at hst
    exact ht‚ÇÇ.of_isClosed_subset isClosed_closure (closure_minimal hst ht‚ÇÅ)
  ¬∑ intro h
    exact ‚ü®closure s, isClosed_closure, h, compl_subset_compl.mpr subset_closure‚ü©
#align bornology.relatively_compact.is_bounded_iff Bornology.relativelyCompact.isBounded_iff

protected theorem Finset.isClosed [T1Space Œ±] (s : Finset Œ±) : IsClosed (s : Set Œ±) :=
  s.finite_toSet.isClosed
#align finset.is_closed Finset.isClosed

theorem t1Space_TFAE (Œ± : Type u) [ TopologicalSpace Œ± ] :
    List.TFAE [T1Space Œ±,
      ‚àÄ x, IsClosed ({ x } : Set Œ±),
      ‚àÄ x, IsOpen ({ x }·∂ú : Set Œ±),
      Continuous (@CofiniteTopology.of Œ±),
      ‚àÄ ‚¶Éx y : Œ±‚¶Ñ, x ‚â† y ‚Üí {y}·∂ú ‚àà ùìù x,
      ‚àÄ ‚¶Éx y : Œ±‚¶Ñ, x ‚â† y ‚Üí ‚àÉ s ‚àà ùìù x, y ‚àâ s,
      ‚àÄ ‚¶Éx y : Œ±‚¶Ñ, x ‚â† y ‚Üí ‚àÉ U : Set Œ±, IsOpen U ‚àß x ‚àà U ‚àß y ‚àâ U,
      ‚àÄ ‚¶Éx y : Œ±‚¶Ñ, x ‚â† y ‚Üí Disjoint (ùìù x) (pure y),
      ‚àÄ ‚¶Éx y : Œ±‚¶Ñ, x ‚â† y ‚Üí Disjoint (pure x) (ùìù y),
      ‚àÄ ‚¶Éx y : Œ±‚¶Ñ, x ‚§≥ y ‚Üí x = y] := by
  tfae_have 1 ‚Üî 2
  ¬∑ exact ‚ü®fun h => h.1, fun h => ‚ü®h‚ü©‚ü©
  tfae_have 2 ‚Üî 3
  ¬∑ simp only [isOpen_compl_iff]
  tfae_have 5 ‚Üî 3
  ¬∑ refine' forall_swap.trans _
    simp only [isOpen_iff_mem_nhds, mem_compl_iff, mem_singleton_iff]
  tfae_have 5 ‚Üî 6
  ¬∑ simp only [‚Üê subset_compl_singleton_iff, exists_mem_subset_iff]
  tfae_have 5 ‚Üî 7
  ¬∑ simp only [(nhds_basis_opens _).mem_iff, subset_compl_singleton_iff, exists_prop, and_assoc,
      and_left_comm]
  tfae_have 5 ‚Üî 8
  ¬∑ simp only [‚Üê principal_singleton, disjoint_principal_right]
  tfae_have 8 ‚Üî 9
  ¬∑ exact forall_swap.trans (by simp only [disjoint_comm, ne_comm])
  tfae_have 1 ‚Üí 4
  ¬∑ simp only [continuous_def, CofiniteTopology.isOpen_iff']
    rintro H s (rfl | hs)
    exacts [isOpen_empty, compl_compl s ‚ñ∏ (@Set.Finite.isClosed _ _ H _ hs).isOpen_compl]
  tfae_have 4 ‚Üí 2
  ¬∑ exact fun h x => (CofiniteTopology.isClosed_iff.2 <| Or.inr (finite_singleton _)).preimage h
  tfae_have 2 ‚Üî 10
  ¬∑ simp only [‚Üê closure_subset_iff_isClosed, specializes_iff_mem_closure, subset_def,
      mem_singleton_iff, eq_comm]
  tfae_finish
#align t1_space_tfae t1Space_TFAE

theorem t1Space_iff_continuous_cofinite_of {Œ± : Type*} [TopologicalSpace Œ±] :
    T1Space Œ± ‚Üî Continuous (@CofiniteTopology.of Œ±) :=
  (t1Space_TFAE Œ±).out 0 3
#align t1_space_iff_continuous_cofinite_of t1Space_iff_continuous_cofinite_of

theorem CofiniteTopology.continuous_of [T1Space Œ±] : Continuous (@CofiniteTopology.of Œ±) :=
  t1Space_iff_continuous_cofinite_of.mp ‚Äπ_‚Ä∫
#align cofinite_topology.continuous_of CofiniteTopology.continuous_of

theorem t1Space_iff_exists_open :
    T1Space Œ± ‚Üî ‚àÄ x y, x ‚â† y ‚Üí ‚àÉ U : Set Œ±, IsOpen U ‚àß x ‚àà U ‚àß y ‚àâ U :=
  (t1Space_TFAE Œ±).out 0 6
#align t1_space_iff_exists_open t1Space_iff_exists_open

theorem t1Space_iff_disjoint_pure_nhds : T1Space Œ± ‚Üî ‚àÄ ‚¶Éx y : Œ±‚¶Ñ, x ‚â† y ‚Üí Disjoint (pure x) (ùìù y) :=
  (t1Space_TFAE Œ±).out 0 8
#align t1_space_iff_disjoint_pure_nhds t1Space_iff_disjoint_pure_nhds

theorem t1Space_iff_disjoint_nhds_pure : T1Space Œ± ‚Üî ‚àÄ ‚¶Éx y : Œ±‚¶Ñ, x ‚â† y ‚Üí Disjoint (ùìù x) (pure y) :=
  (t1Space_TFAE Œ±).out 0 7
#align t1_space_iff_disjoint_nhds_pure t1Space_iff_disjoint_nhds_pure

theorem t1Space_iff_specializes_imp_eq : T1Space Œ± ‚Üî ‚àÄ ‚¶Éx y : Œ±‚¶Ñ, x ‚§≥ y ‚Üí x = y :=
  (t1Space_TFAE Œ±).out 0 9
#align t1_space_iff_specializes_imp_eq t1Space_iff_specializes_imp_eq

theorem disjoint_pure_nhds [T1Space Œ±] {x y : Œ±} (h : x ‚â† y) : Disjoint (pure x) (ùìù y) :=
  t1Space_iff_disjoint_pure_nhds.mp ‚Äπ_‚Ä∫ h
#align disjoint_pure_nhds disjoint_pure_nhds

theorem disjoint_nhds_pure [T1Space Œ±] {x y : Œ±} (h : x ‚â† y) : Disjoint (ùìù x) (pure y) :=
  t1Space_iff_disjoint_nhds_pure.mp ‚Äπ_‚Ä∫ h
#align disjoint_nhds_pure disjoint_nhds_pure

theorem Specializes.eq [T1Space Œ±] {x y : Œ±} (h : x ‚§≥ y) : x = y :=
  t1Space_iff_specializes_imp_eq.1 ‚Äπ_‚Ä∫ h
#align specializes.eq Specializes.eq

theorem specializes_iff_eq [T1Space Œ±] {x y : Œ±} : x ‚§≥ y ‚Üî x = y :=
  ‚ü®Specializes.eq, fun h => h ‚ñ∏ specializes_rfl‚ü©
#align specializes_iff_eq specializes_iff_eq

@[simp] theorem specializes_eq_eq [T1Space Œ±] : (¬∑ ‚§≥ ¬∑) = @Eq Œ± :=
  funext‚ÇÇ fun _ _ => propext specializes_iff_eq
#align specializes_eq_eq specializes_eq_eq

@[simp]
theorem pure_le_nhds_iff [T1Space Œ±] {a b : Œ±} : pure a ‚â§ ùìù b ‚Üî a = b :=
  specializes_iff_pure.symm.trans specializes_iff_eq
#align pure_le_nhds_iff pure_le_nhds_iff

@[simp]
theorem nhds_le_nhds_iff [T1Space Œ±] {a b : Œ±} : ùìù a ‚â§ ùìù b ‚Üî a = b :=
  specializes_iff_eq
#align nhds_le_nhds_iff nhds_le_nhds_iff

instance {Œ± : Type*} : T1Space (CofiniteTopology Œ±) :=
  t1Space_iff_continuous_cofinite_of.mpr continuous_id

theorem t1Space_antitone {Œ± : Type*} : Antitone (@T1Space Œ±) := fun a _ h _ =>
  @T1Space.mk _ a fun x => (T1Space.t1 x).mono h
#align t1_space_antitone t1Space_antitone

theorem continuousWithinAt_update_of_ne [T1Space Œ±] [DecidableEq Œ±] [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤}
    {s : Set Œ±} {x y : Œ±} {z : Œ≤} (hne : y ‚â† x) :
    ContinuousWithinAt (Function.update f x z) s y ‚Üî ContinuousWithinAt f s y :=
  EventuallyEq.congr_continuousWithinAt
    (mem_nhdsWithin_of_mem_nhds <| mem_of_superset (isOpen_ne.mem_nhds hne) fun _y' hy' =>
      Function.update_noteq hy' _ _)
    (Function.update_noteq hne _ _)
#align continuous_within_at_update_of_ne continuousWithinAt_update_of_ne

theorem continuousAt_update_of_ne [T1Space Œ±] [DecidableEq Œ±] [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤}
    {x y : Œ±} {z : Œ≤} (hne : y ‚â† x) : ContinuousAt (Function.update f x z) y ‚Üî ContinuousAt f y :=
  by simp only [‚Üê continuousWithinAt_univ, continuousWithinAt_update_of_ne hne]
#align continuous_at_update_of_ne continuousAt_update_of_ne

theorem continuousOn_update_iff [T1Space Œ±] [DecidableEq Œ±] [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤}
    {s : Set Œ±} {x : Œ±} {y : Œ≤} :
    ContinuousOn (Function.update f x y) s ‚Üî
      ContinuousOn f (s \ {x}) ‚àß (x ‚àà s ‚Üí Tendsto f (ùìù[s \ {x}] x) (ùìù y)) := by
  rw [ContinuousOn, ‚Üê and_forall_ne x, and_comm]
  refine' and_congr ‚ü®fun H z hz => _, fun H z hzx hzs => _‚ü© (forall_congr' fun _ => _)
  ¬∑ specialize H z hz.2 hz.1
    rw [continuousWithinAt_update_of_ne hz.2] at H
    exact H.mono (diff_subset _ _)
  ¬∑ rw [continuousWithinAt_update_of_ne hzx]
    refine' (H z ‚ü®hzs, hzx‚ü©).mono_of_mem (inter_mem_nhdsWithin _ _)
    exact isOpen_ne.mem_nhds hzx
  ¬∑ exact continuousWithinAt_update_same
#align continuous_on_update_iff continuousOn_update_iff

theorem t1Space_of_injective_of_continuous [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : Function.Injective f) (hf' : Continuous f) [T1Space Œ≤] : T1Space Œ± :=
  t1Space_iff_specializes_imp_eq.2 fun _ _ h => hf (h.map hf').eq
#align t1_space_of_injective_of_continuous t1Space_of_injective_of_continuous

protected theorem Embedding.t1Space [TopologicalSpace Œ≤] [T1Space Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : Embedding f) : T1Space Œ± :=
  t1Space_of_injective_of_continuous hf.inj hf.continuous
#align embedding.t1_space Embedding.t1Space

instance Subtype.t1Space {Œ± : Type u} [TopologicalSpace Œ±] [T1Space Œ±] {p : Œ± ‚Üí Prop} :
    T1Space (Subtype p) :=
  embedding_subtype_val.t1Space
#align subtype.t1_space Subtype.t1Space

instance [TopologicalSpace Œ≤] [T1Space Œ±] [T1Space Œ≤] : T1Space (Œ± √ó Œ≤) :=
  ‚ü®fun ‚ü®a, b‚ü© => @singleton_prod_singleton _ _ a b ‚ñ∏ isClosed_singleton.prod isClosed_singleton‚ü©

instance {Œπ : Type*} {œÄ : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (œÄ i)] [‚àÄ i, T1Space (œÄ i)] :
    T1Space (‚àÄ i, œÄ i) :=
  ‚ü®fun f => univ_pi_singleton f ‚ñ∏ isClosed_set_pi fun _ _ => isClosed_singleton‚ü©

-- see Note [lower instance priority]
instance (priority := 100) T1Space.t0Space [T1Space Œ±] : T0Space Œ± :=
  ‚ü®fun _ _ h => h.specializes.eq‚ü©
#align t1_space.t0_space T1Space.t0Space

@[simp]
theorem compl_singleton_mem_nhds_iff [T1Space Œ±] {x y : Œ±} : {x}·∂ú ‚àà ùìù y ‚Üî y ‚â† x :=
  isOpen_compl_singleton.mem_nhds_iff
#align compl_singleton_mem_nhds_iff compl_singleton_mem_nhds_iff

theorem compl_singleton_mem_nhds [T1Space Œ±] {x y : Œ±} (h : y ‚â† x) : {x}·∂ú ‚àà ùìù y :=
  compl_singleton_mem_nhds_iff.mpr h
#align compl_singleton_mem_nhds compl_singleton_mem_nhds

@[simp]
theorem closure_singleton [T1Space Œ±] {a : Œ±} : closure ({a} : Set Œ±) = {a} :=
  isClosed_singleton.closure_eq
#align closure_singleton closure_singleton

-- porting note: todo: the proof was `hs.induction_on (by simp) fun x => by simp`
theorem Set.Subsingleton.closure [T1Space Œ±] {s : Set Œ±} (hs : s.Subsingleton) :
    (closure s).Subsingleton := by
  rcases hs.eq_empty_or_singleton with (rfl | ‚ü®x, rfl‚ü©) <;> simp
#align set.subsingleton.closure Set.Subsingleton.closure

@[simp]
theorem subsingleton_closure [T1Space Œ±] {s : Set Œ±} : (closure s).Subsingleton ‚Üî s.Subsingleton :=
  ‚ü®fun h => h.anti subset_closure, fun h => h.closure‚ü©
#align subsingleton_closure subsingleton_closure

theorem isClosedMap_const {Œ± Œ≤} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [T1Space Œ≤] {y : Œ≤} :
    IsClosedMap (Function.const Œ± y) :=
  IsClosedMap.of_nonempty fun s _ h2s => by simp_rw [const, h2s.image_const, isClosed_singleton]
#align is_closed_map_const isClosedMap_const

theorem nhdsWithin_insert_of_ne [T1Space Œ±] {x y : Œ±} {s : Set Œ±} (hxy : x ‚â† y) :
    ùìù[insert y s] x = ùìù[s] x := by
  refine' le_antisymm (Filter.le_def.2 fun t ht => _) (nhdsWithin_mono x <| subset_insert y s)
  obtain ‚ü®o, ho, hxo, host‚ü© := mem_nhdsWithin.mp ht
  refine' mem_nhdsWithin.mpr ‚ü®o \ {y}, ho.sdiff isClosed_singleton, ‚ü®hxo, hxy‚ü©, _‚ü©
  rw [inter_insert_of_not_mem <| not_mem_diff_of_mem (mem_singleton y)]
  exact (inter_subset_inter (diff_subset _ _) Subset.rfl).trans host
#align nhds_within_insert_of_ne nhdsWithin_insert_of_ne

/-- If `t` is a subset of `s`, except for one point,
then `insert x s` is a neighborhood of `x` within `t`. -/
theorem insert_mem_nhdsWithin_of_subset_insert [T1Space Œ±] {x y : Œ±} {s t : Set Œ±}
    (hu : t ‚äÜ insert y s) : insert x s ‚àà ùìù[t] x := by
  rcases eq_or_ne x y with (rfl | h)
  ¬∑ exact mem_of_superset self_mem_nhdsWithin hu
  refine' nhdsWithin_mono x hu _
  rw [nhdsWithin_insert_of_ne h]
  exact mem_of_superset self_mem_nhdsWithin (subset_insert x s)
#align insert_mem_nhds_within_of_subset_insert insert_mem_nhdsWithin_of_subset_insert

@[simp]
theorem ker_nhds [T1Space Œ±] (x : Œ±) : (ùìù x).ker = {x} := by
  simp [ker_nhds_eq_specializes]

theorem biInter_basis_nhds [T1Space Œ±] {Œπ : Sort*} {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí Set Œ±} {x : Œ±}
    (h : (ùìù x).HasBasis p s) : ‚ãÇ (i) (_ : p i), s i = {x} := by
  rw [‚Üê h.ker, ker_nhds]
#align bInter_basis_nhds biInter_basis_nhds

@[simp]
theorem compl_singleton_mem_nhdsSet_iff [T1Space Œ±] {x : Œ±} {s : Set Œ±} : {x}·∂ú ‚àà ùìùÀ¢ s ‚Üî x ‚àâ s := by
  rw [isOpen_compl_singleton.mem_nhdsSet, subset_compl_singleton_iff]
#align compl_singleton_mem_nhds_set_iff compl_singleton_mem_nhdsSet_iff

@[simp]
theorem nhdsSet_le_iff [T1Space Œ±] {s t : Set Œ±} : ùìùÀ¢ s ‚â§ ùìùÀ¢ t ‚Üî s ‚äÜ t := by
  refine' ‚ü®_, fun h => monotone_nhdsSet h‚ü©
  simp_rw [Filter.le_def]; intro h x hx
  specialize h {x}·∂ú
  simp_rw [compl_singleton_mem_nhdsSet_iff] at h
  by_contra hxt
  exact h hxt hx
#align nhds_set_le_iff nhdsSet_le_iff

@[simp]
theorem nhdsSet_inj_iff [T1Space Œ±] {s t : Set Œ±} : ùìùÀ¢ s = ùìùÀ¢ t ‚Üî s = t := by
  simp_rw [le_antisymm_iff]
  exact and_congr nhdsSet_le_iff nhdsSet_le_iff
#align nhds_set_inj_iff nhdsSet_inj_iff

theorem injective_nhdsSet [T1Space Œ±] : Function.Injective (ùìùÀ¢ : Set Œ± ‚Üí Filter Œ±) := fun _ _ hst =>
  nhdsSet_inj_iff.mp hst
#align injective_nhds_set injective_nhdsSet

theorem strictMono_nhdsSet [T1Space Œ±] : StrictMono (ùìùÀ¢ : Set Œ± ‚Üí Filter Œ±) :=
  monotone_nhdsSet.strictMono_of_injective injective_nhdsSet
#align strict_mono_nhds_set strictMono_nhdsSet

@[simp]
theorem nhds_le_nhdsSet_iff [T1Space Œ±] {s : Set Œ±} {x : Œ±} : ùìù x ‚â§ ùìùÀ¢ s ‚Üî x ‚àà s := by
  rw [‚Üê nhdsSet_singleton, nhdsSet_le_iff, singleton_subset_iff]
#align nhds_le_nhds_set_iff nhds_le_nhdsSet_iff

/-- Removing a non-isolated point from a dense set, one still obtains a dense set. -/
theorem Dense.diff_singleton [T1Space Œ±] {s : Set Œ±} (hs : Dense s) (x : Œ±) [NeBot (ùìù[‚â†] x)] :
    Dense (s \ {x}) :=
  hs.inter_of_open_right (dense_compl_singleton x) isOpen_compl_singleton
#align dense.diff_singleton Dense.diff_singleton

/-- Removing a finset from a dense set in a space without isolated points, one still
obtains a dense set. -/
theorem Dense.diff_finset [T1Space Œ±] [‚àÄ x : Œ±, NeBot (ùìù[‚â†] x)] {s : Set Œ±} (hs : Dense s)
    (t : Finset Œ±) : Dense (s \ t) := by
  induction t using Finset.induction_on with
  | empty => simpa using hs
  | insert _ ih =>
    rw [Finset.coe_insert, ‚Üê union_singleton, ‚Üê diff_diff]
    exact ih.diff_singleton _
#align dense.diff_finset Dense.diff_finset

/-- Removing a finite set from a dense set in a space without isolated points, one still
obtains a dense set. -/
theorem Dense.diff_finite [T1Space Œ±] [‚àÄ x : Œ±, NeBot (ùìù[‚â†] x)] {s : Set Œ±} (hs : Dense s)
    {t : Set Œ±} (ht : t.Finite) : Dense (s \ t) := by
  convert hs.diff_finset ht.toFinset
  exact (Finite.coe_toFinset _).symm
#align dense.diff_finite Dense.diff_finite

/-- If a function to a `T1Space` tends to some limit `b` at some point `a`, then necessarily
`b = f a`. -/
theorem eq_of_tendsto_nhds [TopologicalSpace Œ≤] [T1Space Œ≤] {f : Œ± ‚Üí Œ≤} {a : Œ±} {b : Œ≤}
    (h : Tendsto f (ùìù a) (ùìù b)) : f a = b :=
  by_contra fun hfa : f a ‚â† b =>
    have fact‚ÇÅ : {f a}·∂ú ‚àà ùìù b := compl_singleton_mem_nhds hfa.symm
    have fact‚ÇÇ : Tendsto f (pure a) (ùìù b) := h.comp (tendsto_id'.2 <| pure_le_nhds a)
    fact‚ÇÇ fact‚ÇÅ (Eq.refl <| f a)
#align eq_of_tendsto_nhds eq_of_tendsto_nhds

theorem Filter.Tendsto.eventually_ne [TopologicalSpace Œ≤] [T1Space Œ≤] {Œ± : Type*} {g : Œ± ‚Üí Œ≤}
    {l : Filter Œ±} {b‚ÇÅ b‚ÇÇ : Œ≤} (hg : Tendsto g l (ùìù b‚ÇÅ)) (hb : b‚ÇÅ ‚â† b‚ÇÇ) : ‚àÄ·∂† z in l, g z ‚â† b‚ÇÇ :=
  hg.eventually (isOpen_compl_singleton.eventually_mem hb)
#align filter.tendsto.eventually_ne Filter.Tendsto.eventually_ne

theorem ContinuousAt.eventually_ne [TopologicalSpace Œ≤] [T1Space Œ≤] {g : Œ± ‚Üí Œ≤} {a : Œ±} {b : Œ≤}
    (hg1 : ContinuousAt g a) (hg2 : g a ‚â† b) : ‚àÄ·∂† z in ùìù a, g z ‚â† b :=
  hg1.tendsto.eventually_ne hg2
#align continuous_at.eventually_ne ContinuousAt.eventually_ne

theorem eventually_ne_nhds [T1Space Œ±] {a b : Œ±} (h : a ‚â† b) : ‚àÄ·∂† x in ùìù a, x ‚â† b :=
  IsOpen.eventually_mem isOpen_ne h

theorem eventually_ne_nhdsWithin [T1Space Œ±] {a b : Œ±} {s : Set Œ±} (h : a ‚â† b) :
    ‚àÄ·∂† x in ùìù[s] a, x ‚â† b :=
  Filter.Eventually.filter_mono nhdsWithin_le_nhds <| eventually_ne_nhds h

/-- To prove a function to a `T1Space` is continuous at some point `a`, it suffices to prove that
`f` admits *some* limit at `a`. -/
theorem continuousAt_of_tendsto_nhds [TopologicalSpace Œ≤] [T1Space Œ≤] {f : Œ± ‚Üí Œ≤} {a : Œ±} {b : Œ≤}
    (h : Tendsto f (ùìù a) (ùìù b)) : ContinuousAt f a := by
  rwa [ContinuousAt, eq_of_tendsto_nhds h]
#align continuous_at_of_tendsto_nhds continuousAt_of_tendsto_nhds

@[simp]
theorem tendsto_const_nhds_iff [T1Space Œ±] {l : Filter Œ≤} [NeBot l] {c d : Œ±} :
    Tendsto (fun _ => c) l (ùìù d) ‚Üî c = d := by simp_rw [Tendsto, Filter.map_const, pure_le_nhds_iff]
#align tendsto_const_nhds_iff tendsto_const_nhds_iff

/-- A point with a finite neighborhood has to be isolated. -/
theorem isOpen_singleton_of_finite_mem_nhds {Œ± : Type*} [TopologicalSpace Œ±] [T1Space Œ±] (x : Œ±)
    {s : Set Œ±} (hs : s ‚àà ùìù x) (hsf : s.Finite) : IsOpen ({x} : Set Œ±) := by
  have A : {x} ‚äÜ s := by simp only [singleton_subset_iff, mem_of_mem_nhds hs]
  have B : IsClosed (s \ {x}) := (hsf.subset (diff_subset _ _)).isClosed
  have C : (s \ {x})·∂ú ‚àà ùìù x := B.isOpen_compl.mem_nhds fun h => h.2 rfl
  have D : {x} ‚àà ùìù x := by simpa only [‚Üê diff_eq, diff_diff_cancel_left A] using inter_mem hs C
  rwa [‚Üê mem_interior_iff_mem_nhds, ‚Üê singleton_subset_iff, subset_interior_iff_isOpen] at D
#align is_open_singleton_of_finite_mem_nhds isOpen_singleton_of_finite_mem_nhds

/-- If the punctured neighborhoods of a point form a nontrivial filter, then any neighborhood is
infinite. -/
theorem infinite_of_mem_nhds {Œ±} [TopologicalSpace Œ±] [T1Space Œ±] (x : Œ±) [hx : NeBot (ùìù[‚â†] x)]
    {s : Set Œ±} (hs : s ‚àà ùìù x) : Set.Infinite s := by
  refine' fun hsf => hx.1 _
  rw [‚Üê isOpen_singleton_iff_punctured_nhds]
  exact isOpen_singleton_of_finite_mem_nhds x hs hsf
#align infinite_of_mem_nhds infinite_of_mem_nhds

theorem discrete_of_t1_of_finite {X : Type*} [TopologicalSpace X] [T1Space X] [Finite X] :
    DiscreteTopology X := by
  apply singletons_open_iff_discrete.mp
  intro x
  rw [‚Üê isClosed_compl_iff]
  exact (Set.toFinite _).isClosed
#align discrete_of_t1_of_finite discrete_of_t1_of_finite

theorem PreconnectedSpace.trivial_of_discrete [PreconnectedSpace Œ±] [DiscreteTopology Œ±] :
    Subsingleton Œ± := by
  rw [‚Üê not_nontrivial_iff_subsingleton]
  rintro ‚ü®x, y, hxy‚ü©
  rw [Ne.def, ‚Üê mem_singleton_iff, (isClopen_discrete _).eq_univ <| singleton_nonempty y] at hxy
  exact hxy (mem_univ x)
#align preconnected_space.trivial_of_discrete PreconnectedSpace.trivial_of_discrete

theorem IsPreconnected.infinite_of_nontrivial [T1Space Œ±] {s : Set Œ±} (h : IsPreconnected s)
    (hs : s.Nontrivial) : s.Infinite := by
  refine' mt (fun hf => (subsingleton_coe s).mp _) (not_subsingleton_iff.mpr hs)
  haveI := @discrete_of_t1_of_finite s _ _ hf.to_subtype
  exact @PreconnectedSpace.trivial_of_discrete _ _ (Subtype.preconnectedSpace h) _
#align is_preconnected.infinite_of_nontrivial IsPreconnected.infinite_of_nontrivial

theorem ConnectedSpace.infinite [ConnectedSpace Œ±] [Nontrivial Œ±] [T1Space Œ±] : Infinite Œ± :=
  infinite_univ_iff.mp <| isPreconnected_univ.infinite_of_nontrivial nontrivial_univ
#align connected_space.infinite ConnectedSpace.infinite

/-- A non-trivial connected T1 space has no isolated points. -/
instance ConnectedSpace.neBot_nhdsWithin_compl_of_nontrivial_of_t1space
    [ConnectedSpace Œ±] [Nontrivial Œ±] [T1Space Œ±] (x : Œ±) :
    NeBot (ùìù[‚â†] x) := by
  by_contra contra
  rw [not_neBot, ‚Üê isOpen_singleton_iff_punctured_nhds] at contra
  replace contra := nonempty_inter isOpen_compl_singleton
    contra (compl_union_self _) (Set.nonempty_compl_of_nontrivial _) (singleton_nonempty _)
  simp [compl_inter_self {x}] at contra

theorem singleton_mem_nhdsWithin_of_mem_discrete {s : Set Œ±} [DiscreteTopology s] {x : Œ±}
    (hx : x ‚àà s) : {x} ‚àà ùìù[s] x := by
  have : ({‚ü®x, hx‚ü©} : Set s) ‚àà ùìù (‚ü®x, hx‚ü© : s) := by simp [nhds_discrete]
  simpa only [nhdsWithin_eq_map_subtype_coe hx, image_singleton] using
    @image_mem_map _ _ _ ((‚Üë) : s ‚Üí Œ±) _ this
#align singleton_mem_nhds_within_of_mem_discrete singleton_mem_nhdsWithin_of_mem_discrete

/-- The neighbourhoods filter of `x` within `s`, under the discrete topology, is equal to
the pure `x` filter (which is the principal filter at the singleton `{x}`.) -/
theorem nhdsWithin_of_mem_discrete {s : Set Œ±} [DiscreteTopology s] {x : Œ±} (hx : x ‚àà s) :
    ùìù[s] x = pure x :=
  le_antisymm (le_pure_iff.2 <| singleton_mem_nhdsWithin_of_mem_discrete hx) (pure_le_nhdsWithin hx)
#align nhds_within_of_mem_discrete nhdsWithin_of_mem_discrete

theorem Filter.HasBasis.exists_inter_eq_singleton_of_mem_discrete {Œπ : Type*} {p : Œπ ‚Üí Prop}
    {t : Œπ ‚Üí Set Œ±} {s : Set Œ±} [DiscreteTopology s] {x : Œ±} (hb : (ùìù x).HasBasis p t)
    (hx : x ‚àà s) : ‚àÉ i, p i ‚àß t i ‚à© s = {x} := by
  rcases (nhdsWithin_hasBasis hb s).mem_iff.1 (singleton_mem_nhdsWithin_of_mem_discrete hx) with
    ‚ü®i, hi, hix‚ü©
  exact ‚ü®i, hi, hix.antisymm <| singleton_subset_iff.2 ‚ü®mem_of_mem_nhds <| hb.mem_of_mem hi, hx‚ü©‚ü©
#align filter.has_basis.exists_inter_eq_singleton_of_mem_discrete Filter.HasBasis.exists_inter_eq_singleton_of_mem_discrete

/-- A point `x` in a discrete subset `s` of a topological space admits a neighbourhood
that only meets `s` at `x`.  -/
theorem nhds_inter_eq_singleton_of_mem_discrete {s : Set Œ±} [DiscreteTopology s] {x : Œ±}
    (hx : x ‚àà s) : ‚àÉ U ‚àà ùìù x, U ‚à© s = {x} := by
  simpa using (ùìù x).basis_sets.exists_inter_eq_singleton_of_mem_discrete hx
#align nhds_inter_eq_singleton_of_mem_discrete nhds_inter_eq_singleton_of_mem_discrete

/-- For point `x` in a discrete subset `s` of a topological space, there is a set `U`
such that
1. `U` is a punctured neighborhood of `x` (ie. `U ‚à™ {x}` is a neighbourhood of `x`),
2. `U` is disjoint from `s`.
-/
theorem disjoint_nhdsWithin_of_mem_discrete {s : Set Œ±} [DiscreteTopology s] {x : Œ±} (hx : x ‚àà s) :
    ‚àÉ U ‚àà ùìù[‚â†] x, Disjoint U s :=
  let ‚ü®V, h, h'‚ü© := nhds_inter_eq_singleton_of_mem_discrete hx
  ‚ü®{x}·∂ú ‚à© V, inter_mem_nhdsWithin _ h,
    disjoint_iff_inter_eq_empty.mpr (by rw [inter_assoc, h', compl_inter_self])‚ü©
#align disjoint_nhds_within_of_mem_discrete disjoint_nhdsWithin_of_mem_discrete

/-- Let `X` be a topological space and let `s, t ‚äÜ X` be two subsets.  If there is an inclusion
`t ‚äÜ s`, then the topological space structure on `t` induced by `X` is the same as the one
obtained by the induced topological space structure on `s`. Use `embedding_inclusion` instead. -/
@[deprecated embedding_inclusion]
theorem TopologicalSpace.subset_trans {X : Type*} [TopologicalSpace X] {s t : Set X}
    (ts : t ‚äÜ s) :
    (instTopologicalSpaceSubtype : TopologicalSpace t) =
      (instTopologicalSpaceSubtype : TopologicalSpace s).induced (Set.inclusion ts) :=
  (embedding_inclusion ts).induced
#align topological_space.subset_trans TopologicalSpace.subset_trans

/-- A T‚ÇÇ space, also known as a Hausdorff space, is one in which for every
  `x ‚â† y` there exists disjoint open sets around `x` and `y`. This is
  the most widely used of the separation axioms. -/
@[mk_iff t2Space_iff]
class T2Space (Œ± : Type u) [TopologicalSpace Œ±] : Prop where
  /-- Every two points in a Hausdorff space admit disjoint open neighbourhoods. -/
  t2 : ‚àÄ x y, x ‚â† y ‚Üí ‚àÉ u v : Set Œ±, IsOpen u ‚àß IsOpen v ‚àß x ‚àà u ‚àß y ‚àà v ‚àß Disjoint u v
#align t2_space T2Space

/-- Two different points can be separated by open sets. -/
theorem t2_separation [T2Space Œ±] {x y : Œ±} (h : x ‚â† y) :
    ‚àÉ u v : Set Œ±, IsOpen u ‚àß IsOpen v ‚àß x ‚àà u ‚àß y ‚àà v ‚àß Disjoint u v :=
  T2Space.t2 x y h
#align t2_separation t2_separation

-- todo: use this as a definition?
theorem t2Space_iff_disjoint_nhds : T2Space Œ± ‚Üî ‚àÄ x y : Œ±, x ‚â† y ‚Üí Disjoint (ùìù x) (ùìù y) := by
  refine (t2Space_iff Œ±).trans (forall‚ÇÉ_congr fun x y _ => ?_)
  simp only [(nhds_basis_opens x).disjoint_iff (nhds_basis_opens y), exists_prop, ‚Üê exists_and_left,
    and_assoc, and_comm, and_left_comm]
#align t2_space_iff_disjoint_nhds t2Space_iff_disjoint_nhds

@[simp]
theorem disjoint_nhds_nhds [T2Space Œ±] {x y : Œ±} : Disjoint (ùìù x) (ùìù y) ‚Üî x ‚â† y :=
  ‚ü®fun hd he => by simp [he, nhds_neBot.ne] at hd, t2Space_iff_disjoint_nhds.mp ‚Äπ_‚Ä∫ x y‚ü©
#align disjoint_nhds_nhds disjoint_nhds_nhds

theorem pairwise_disjoint_nhds [T2Space Œ±] : Pairwise (Disjoint on (ùìù : Œ± ‚Üí Filter Œ±)) := fun _ _ =>
  disjoint_nhds_nhds.2
#align pairwise_disjoint_nhds pairwise_disjoint_nhds

protected theorem Set.pairwiseDisjoint_nhds [T2Space Œ±] (s : Set Œ±) : s.PairwiseDisjoint ùìù :=
  pairwise_disjoint_nhds.set_pairwise s
#align set.pairwise_disjoint_nhds Set.pairwiseDisjoint_nhds

/-- Points of a finite set can be separated by open sets from each other. -/
theorem Set.Finite.t2_separation [T2Space Œ±] {s : Set Œ±} (hs : s.Finite) :
    ‚àÉ U : Œ± ‚Üí Set Œ±, (‚àÄ x, x ‚àà U x ‚àß IsOpen (U x)) ‚àß s.PairwiseDisjoint U :=
  s.pairwiseDisjoint_nhds.exists_mem_filter_basis hs nhds_basis_opens
#align set.finite.t2_separation Set.Finite.t2_separation

theorem isOpen_setOf_disjoint_nhds_nhds : IsOpen { p : Œ± √ó Œ± | Disjoint (ùìù p.1) (ùìù p.2) } := by
  simp only [isOpen_iff_mem_nhds, Prod.forall, mem_setOf_eq]
  intro x y h
  obtain ‚ü®U, hU, V, hV, hd‚ü© := ((nhds_basis_opens x).disjoint_iff (nhds_basis_opens y)).mp h
  exact
    mem_nhds_prod_iff.mpr
      ‚ü®U, hU.2.mem_nhds hU.1, V, hV.2.mem_nhds hV.1, fun ‚ü®x', y'‚ü© ‚ü®hx', hy'‚ü© =>
        disjoint_of_disjoint_of_mem hd (hU.2.mem_nhds hx') (hV.2.mem_nhds hy')‚ü©
#align is_open_set_of_disjoint_nhds_nhds isOpen_setOf_disjoint_nhds_nhds

-- see Note [lower instance priority]
instance (priority := 100) T2Space.t1Space [T2Space Œ±] : T1Space Œ± :=
  t1Space_iff_disjoint_pure_nhds.mpr fun _ _ hne =>
    (disjoint_nhds_nhds.2 hne).mono_left <| pure_le_nhds _
#align t2_space.t1_space T2Space.t1Space

/-- A space is T‚ÇÇ iff the neighbourhoods of distinct points generate the bottom filter. -/
theorem t2_iff_nhds : T2Space Œ± ‚Üî ‚àÄ {x y : Œ±}, NeBot (ùìù x ‚äì ùìù y) ‚Üí x = y := by
  simp only [t2Space_iff_disjoint_nhds, disjoint_iff, neBot_iff, Ne.def, not_imp_comm]
#align t2_iff_nhds t2_iff_nhds

theorem eq_of_nhds_neBot [T2Space Œ±] {x y : Œ±} (h : NeBot (ùìù x ‚äì ùìù y)) : x = y :=
  t2_iff_nhds.mp ‚Äπ_‚Ä∫ h
#align eq_of_nhds_ne_bot eq_of_nhds_neBot

theorem t2Space_iff_nhds : T2Space Œ± ‚Üî ‚àÄ {x y : Œ±}, x ‚â† y ‚Üí ‚àÉ U ‚àà ùìù x, ‚àÉ V ‚àà ùìù y, Disjoint U V := by
  simp only [t2Space_iff_disjoint_nhds, Filter.disjoint_iff]
#align t2_space_iff_nhds t2Space_iff_nhds

theorem t2_separation_nhds [T2Space Œ±] {x y : Œ±} (h : x ‚â† y) :
    ‚àÉ u v, u ‚àà ùìù x ‚àß v ‚àà ùìù y ‚àß Disjoint u v :=
  let ‚ü®u, v, open_u, open_v, x_in, y_in, huv‚ü© := t2_separation h
  ‚ü®u, v, open_u.mem_nhds x_in, open_v.mem_nhds y_in, huv‚ü©
#align t2_separation_nhds t2_separation_nhds

theorem t2_separation_compact_nhds [LocallyCompactSpace Œ±] [T2Space Œ±] {x y : Œ±} (h : x ‚â† y) :
    ‚àÉ u v, u ‚àà ùìù x ‚àß v ‚àà ùìù y ‚àß IsCompact u ‚àß IsCompact v ‚àß Disjoint u v := by
  simpa only [exists_prop, ‚Üê exists_and_left, and_comm, and_assoc, and_left_comm] using
    ((compact_basis_nhds x).disjoint_iff (compact_basis_nhds y)).1 (disjoint_nhds_nhds.2 h)
#align t2_separation_compact_nhds t2_separation_compact_nhds

theorem t2_iff_ultrafilter :
    T2Space Œ± ‚Üî ‚àÄ {x y : Œ±} (f : Ultrafilter Œ±), ‚Üëf ‚â§ ùìù x ‚Üí ‚Üëf ‚â§ ùìù y ‚Üí x = y :=
  t2_iff_nhds.trans <| by simp only [‚Üê exists_ultrafilter_iff, and_imp, le_inf_iff, exists_imp]
#align t2_iff_ultrafilter t2_iff_ultrafilter

theorem t2_iff_isClosed_diagonal : T2Space Œ± ‚Üî IsClosed (diagonal Œ±) := by
  simp only [t2Space_iff_disjoint_nhds, ‚Üê isOpen_compl_iff, isOpen_iff_mem_nhds, Prod.forall,
    nhds_prod_eq, compl_diagonal_mem_prod, mem_compl_iff, mem_diagonal_iff]
#align t2_iff_is_closed_diagonal t2_iff_isClosed_diagonal

theorem isClosed_diagonal [T2Space Œ±] : IsClosed (diagonal Œ±) :=
  t2_iff_isClosed_diagonal.mp ‚Äπ_‚Ä∫
#align is_closed_diagonal isClosed_diagonal

-- porting note: 2 lemmas moved below

theorem tendsto_nhds_unique [T2Space Œ±] {f : Œ≤ ‚Üí Œ±} {l : Filter Œ≤} {a b : Œ±} [NeBot l]
    (ha : Tendsto f l (ùìù a)) (hb : Tendsto f l (ùìù b)) : a = b :=
  eq_of_nhds_neBot <| neBot_of_le <| le_inf ha hb
#align tendsto_nhds_unique tendsto_nhds_unique

theorem tendsto_nhds_unique' [T2Space Œ±] {f : Œ≤ ‚Üí Œ±} {l : Filter Œ≤} {a b : Œ±} (_ : NeBot l)
    (ha : Tendsto f l (ùìù a)) (hb : Tendsto f l (ùìù b)) : a = b :=
  eq_of_nhds_neBot <| neBot_of_le <| le_inf ha hb
#align tendsto_nhds_unique' tendsto_nhds_unique'

theorem tendsto_nhds_unique_of_eventuallyEq [T2Space Œ±] {f g : Œ≤ ‚Üí Œ±} {l : Filter Œ≤} {a b : Œ±}
    [NeBot l] (ha : Tendsto f l (ùìù a)) (hb : Tendsto g l (ùìù b)) (hfg : f =·∂†[l] g) : a = b :=
  tendsto_nhds_unique (ha.congr' hfg) hb
#align tendsto_nhds_unique_of_eventually_eq tendsto_nhds_unique_of_eventuallyEq

theorem tendsto_nhds_unique_of_frequently_eq [T2Space Œ±] {f g : Œ≤ ‚Üí Œ±} {l : Filter Œ≤} {a b : Œ±}
    (ha : Tendsto f l (ùìù a)) (hb : Tendsto g l (ùìù b)) (hfg : ‚àÉ·∂† x in l, f x = g x) : a = b :=
  have : ‚àÉ·∂† z : Œ± √ó Œ± in ùìù (a, b), z.1 = z.2 := (ha.prod_mk_nhds hb).frequently hfg
  not_not.1 fun hne => this (isClosed_diagonal.isOpen_compl.mem_nhds hne)
#align tendsto_nhds_unique_of_frequently_eq tendsto_nhds_unique_of_frequently_eq

/-- A T‚ÇÇ.‚ÇÖ space, also known as a Urysohn space, is a topological space
  where for every pair `x ‚â† y`, there are two open sets, with the intersection of closures
  empty, one containing `x` and the other `y` . -/
class T25Space (Œ± : Type u) [TopologicalSpace Œ±] : Prop where
  /-- Given two distinct points in a T‚ÇÇ.‚ÇÖ space, their filters of closed neighborhoods are
  disjoint. -/
  t2_5 : ‚àÄ ‚¶Éx y : Œ±‚¶Ñ, x ‚â† y ‚Üí Disjoint ((ùìù x).lift' closure) ((ùìù y).lift' closure)
#align t2_5_space T25Space

@[simp]
theorem disjoint_lift'_closure_nhds [T25Space Œ±] {x y : Œ±} :
    Disjoint ((ùìù x).lift' closure) ((ùìù y).lift' closure) ‚Üî x ‚â† y :=
  ‚ü®fun h hxy => by simp [hxy, nhds_neBot.ne] at h, fun h => T25Space.t2_5 h‚ü©
#align disjoint_lift'_closure_nhds disjoint_lift'_closure_nhds

-- see Note [lower instance priority]
instance (priority := 100) T25Space.t2Space [T25Space Œ±] : T2Space Œ± :=
  t2Space_iff_disjoint_nhds.2 fun _ _ hne =>
    (disjoint_lift'_closure_nhds.2 hne).mono (le_lift'_closure _) (le_lift'_closure _)
#align t2_5_space.t2_space T25Space.t2Space

theorem exists_nhds_disjoint_closure [T25Space Œ±] {x y : Œ±} (h : x ‚â† y) :
    ‚àÉ s ‚àà ùìù x, ‚àÉ t ‚àà ùìù y, Disjoint (closure s) (closure t) :=
  ((ùìù x).basis_sets.lift'_closure.disjoint_iff (ùìù y).basis_sets.lift'_closure).1 <|
    disjoint_lift'_closure_nhds.2 h
#align exists_nhds_disjoint_closure exists_nhds_disjoint_closure

theorem exists_open_nhds_disjoint_closure [T25Space Œ±] {x y : Œ±} (h : x ‚â† y) :
    ‚àÉ u : Set Œ±,
      x ‚àà u ‚àß IsOpen u ‚àß ‚àÉ v : Set Œ±, y ‚àà v ‚àß IsOpen v ‚àß Disjoint (closure u) (closure v) := by
  simpa only [exists_prop, and_assoc] using
    ((nhds_basis_opens x).lift'_closure.disjoint_iff (nhds_basis_opens y).lift'_closure).1
      (disjoint_lift'_closure_nhds.2 h)
#align exists_open_nhds_disjoint_closure exists_open_nhds_disjoint_closure

section limUnder

variable [T2Space Œ±] {f : Filter Œ±}

/-!
### Properties of `lim` and `limUnder`

In this section we use explicit `Nonempty Œ±` instances for `lim` and `limUnder`. This way the lemmas
are useful without a `Nonempty Œ±` instance.
-/


theorem lim_eq {a : Œ±} [NeBot f] (h : f ‚â§ ùìù a) : @lim _ _ ‚ü®a‚ü© f = a :=
  tendsto_nhds_unique (le_nhds_lim ‚ü®a, h‚ü©) h
set_option linter.uppercaseLean3 false in
#align Lim_eq lim_eq

theorem lim_eq_iff [NeBot f] (h : ‚àÉ a : Œ±, f ‚â§ nhds a) {a} : @lim _ _ ‚ü®a‚ü© f = a ‚Üî f ‚â§ ùìù a :=
  ‚ü®fun c => c ‚ñ∏ le_nhds_lim h, lim_eq‚ü©
set_option linter.uppercaseLean3 false in
#align Lim_eq_iff lim_eq_iff

theorem Ultrafilter.lim_eq_iff_le_nhds [CompactSpace Œ±] {x : Œ±} {F : Ultrafilter Œ±} :
    F.lim = x ‚Üî ‚ÜëF ‚â§ ùìù x :=
  ‚ü®fun h => h ‚ñ∏ F.le_nhds_lim, lim_eq‚ü©
set_option linter.uppercaseLean3 false in
#align ultrafilter.Lim_eq_iff_le_nhds Ultrafilter.lim_eq_iff_le_nhds

theorem isOpen_iff_ultrafilter' [CompactSpace Œ±] (U : Set Œ±) :
    IsOpen U ‚Üî ‚àÄ F : Ultrafilter Œ±, F.lim ‚àà U ‚Üí U ‚àà F.1 := by
  rw [isOpen_iff_ultrafilter]
  refine' ‚ü®fun h F hF => h F.lim hF F F.le_nhds_lim, _‚ü©
  intro cond x hx f h
  rw [‚Üê Ultrafilter.lim_eq_iff_le_nhds.2 h] at hx
  exact cond _ hx
#align is_open_iff_ultrafilter' isOpen_iff_ultrafilter'

theorem Filter.Tendsto.limUnder_eq {a : Œ±} {f : Filter Œ≤} [NeBot f] {g : Œ≤ ‚Üí Œ±}
    (h : Tendsto g f (ùìù a)) : @limUnder _ _ _ ‚ü®a‚ü© f g = a :=
  lim_eq h
#align filter.tendsto.lim_eq Filter.Tendsto.limUnder_eq

theorem Filter.limUnder_eq_iff {f : Filter Œ≤} [NeBot f] {g : Œ≤ ‚Üí Œ±} (h : ‚àÉ a, Tendsto g f (ùìù a))
    {a} : @limUnder _ _ _ ‚ü®a‚ü© f g = a ‚Üî Tendsto g f (ùìù a) :=
  ‚ü®fun c => c ‚ñ∏ tendsto_nhds_limUnder h, Filter.Tendsto.limUnder_eq‚ü©
#align filter.lim_eq_iff Filter.limUnder_eq_iff

theorem Continuous.limUnder_eq [TopologicalSpace Œ≤] {f : Œ≤ ‚Üí Œ±} (h : Continuous f) (a : Œ≤) :
    @limUnder _ _ _ ‚ü®f a‚ü© (ùìù a) f = f a :=
  (h.tendsto a).limUnder_eq
#align continuous.lim_eq Continuous.limUnder_eq

@[simp]
theorem lim_nhds (a : Œ±) : @lim _ _ ‚ü®a‚ü© (ùìù a) = a :=
  lim_eq le_rfl
set_option linter.uppercaseLean3 false in
#align Lim_nhds lim_nhds

@[simp]
theorem limUnder_nhds_id (a : Œ±) : @limUnder _ _ _ ‚ü®a‚ü© (ùìù a) id = a :=
  lim_nhds a
#align lim_nhds_id limUnder_nhds_id

@[simp]
theorem lim_nhdsWithin {a : Œ±} {s : Set Œ±} (h : a ‚àà closure s) : @lim _ _ ‚ü®a‚ü© (ùìù[s] a) = a :=
  haveI : NeBot (ùìù[s] a) := mem_closure_iff_clusterPt.1 h
  lim_eq inf_le_left
set_option linter.uppercaseLean3 false in
#align Lim_nhds_within lim_nhdsWithin

@[simp]
theorem limUnder_nhdsWithin_id {a : Œ±} {s : Set Œ±} (h : a ‚àà closure s) :
    @limUnder _ _ _ ‚ü®a‚ü© (ùìù[s] a) id = a :=
  lim_nhdsWithin h
#align lim_nhds_within_id limUnder_nhdsWithin_id

end limUnder

/-!
### `T2Space` constructions

We use two lemmas to prove that various standard constructions generate Hausdorff spaces from
Hausdorff spaces:

* `separated_by_continuous` says that two points `x y : Œ±` can be separated by open neighborhoods
  provided that there exists a continuous map `f : Œ± ‚Üí Œ≤` with a Hausdorff codomain such that
  `f x ‚â† f y`. We use this lemma to prove that topological spaces defined using `induced` are
  Hausdorff spaces.

* `separated_by_openEmbedding` says that for an open embedding `f : Œ± ‚Üí Œ≤` of a Hausdorff space
  `Œ±`, the images of two distinct points `x y : Œ±`, `x ‚â† y` can be separated by open neighborhoods.
  We use this lemma to prove that topological spaces defined using `coinduced` are Hausdorff spaces.
-/


-- see Note [lower instance priority]
instance (priority := 100) DiscreteTopology.toT2Space {Œ± : Type*} [TopologicalSpace Œ±]
    [DiscreteTopology Œ±] : T2Space Œ± :=
  ‚ü®fun x y h => ‚ü®{x}, {y}, isOpen_discrete _, isOpen_discrete _, rfl, rfl, disjoint_singleton.2 h‚ü©‚ü©
#align discrete_topology.to_t2_space DiscreteTopology.toT2Space

theorem separated_by_continuous {Œ± : Type*} {Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤]
    [T2Space Œ≤] {f : Œ± ‚Üí Œ≤} (hf : Continuous f) {x y : Œ±} (h : f x ‚â† f y) :
    ‚àÉ u v : Set Œ±, IsOpen u ‚àß IsOpen v ‚àß x ‚àà u ‚àß y ‚àà v ‚àß Disjoint u v :=
  let ‚ü®u, v, uo, vo, xu, yv, uv‚ü© := t2_separation h
  ‚ü®f ‚Åª¬π' u, f ‚Åª¬π' v, uo.preimage hf, vo.preimage hf, xu, yv, uv.preimage _‚ü©
#align separated_by_continuous separated_by_continuous

theorem separated_by_openEmbedding {Œ± Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤]
    [T2Space Œ±] {f : Œ± ‚Üí Œ≤} (hf : OpenEmbedding f) {x y : Œ±} (h : x ‚â† y) :
    ‚àÉ u v : Set Œ≤, IsOpen u ‚àß IsOpen v ‚àß f x ‚àà u ‚àß f y ‚àà v ‚àß Disjoint u v :=
  let ‚ü®u, v, uo, vo, xu, yv, uv‚ü© := t2_separation h
  ‚ü®f '' u, f '' v, hf.isOpenMap _ uo, hf.isOpenMap _ vo, mem_image_of_mem _ xu,
    mem_image_of_mem _ yv, disjoint_image_of_injective hf.inj uv‚ü©
#align separated_by_open_embedding separated_by_openEmbedding

instance {Œ± : Type*} {p : Œ± ‚Üí Prop} [TopologicalSpace Œ±] [T2Space Œ±] : T2Space (Subtype p) :=
  ‚ü®fun _ _ h => separated_by_continuous continuous_subtype_val (mt Subtype.eq h)‚ü©

instance Prod.t2Space {Œ± : Type*} {Œ≤ : Type*} [TopologicalSpace Œ±] [T2Space Œ±]
    [TopologicalSpace Œ≤] [T2Space Œ≤] : T2Space (Œ± √ó Œ≤) :=
  ‚ü®fun _ _ h => Or.elim (not_and_or.mp (mt Prod.ext_iff.mpr h))
    (fun h‚ÇÅ => separated_by_continuous continuous_fst h‚ÇÅ) fun h‚ÇÇ =>
    separated_by_continuous continuous_snd h‚ÇÇ‚ü©

/-- If the codomain of an injective continuous function is a Hausdorff space, then so is its
domain. -/
theorem T2Space.of_injective_continuous [TopologicalSpace Œ≤] [T2Space Œ≤] {f : Œ± ‚Üí Œ≤}
    (hinj : Injective f) (hc : Continuous f) : T2Space Œ± :=
  ‚ü®fun _ _ h => separated_by_continuous hc (hinj.ne h)‚ü©

/-- If the codomain of a topological embedding is a Hausdorff space, then so is its domain.
See also `T2Space.of_continuous_injective`. -/
theorem Embedding.t2Space [TopologicalSpace Œ≤] [T2Space Œ≤] {f : Œ± ‚Üí Œ≤} (hf : Embedding f) :
    T2Space Œ± :=
  .of_injective_continuous hf.inj hf.continuous
#align embedding.t2_space Embedding.t2Space

instance {Œ± Œ≤ : Type*} [TopologicalSpace Œ±] [T2Space Œ±] [TopologicalSpace Œ≤] [T2Space Œ≤] :
    T2Space (Œ± ‚äï Œ≤) := by
  constructor
  rintro (x | x) (y | y) h
  ¬∑ exact separated_by_openEmbedding openEmbedding_inl <| ne_of_apply_ne _ h
  ¬∑ exact separated_by_continuous continuous_isLeft <| by simp
  ¬∑ exact separated_by_continuous continuous_isLeft <| by simp
  ¬∑ exact separated_by_openEmbedding openEmbedding_inr <| ne_of_apply_ne _ h

instance Pi.t2Space {Œ± : Type*} {Œ≤ : Œ± ‚Üí Type v} [‚àÄ a, TopologicalSpace (Œ≤ a)]
    [‚àÄ a, T2Space (Œ≤ a)] : T2Space (‚àÄ a, Œ≤ a) :=
  ‚ü®fun _ _ h =>
    let ‚ü®i, hi‚ü© := not_forall.mp (mt funext h)
    separated_by_continuous (continuous_apply i) hi‚ü©
#align Pi.t2_space Pi.t2Space

instance Sigma.t2Space {Œπ} {Œ± : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (Œ± i)] [‚àÄ a, T2Space (Œ± a)] :
    T2Space (Œ£i, Œ± i) := by
  constructor
  rintro ‚ü®i, x‚ü© ‚ü®j, y‚ü© neq
  rcases eq_or_ne i j with (rfl | h)
  ¬∑ replace neq : x ‚â† y := ne_of_apply_ne _ neq
    exact separated_by_openEmbedding openEmbedding_sigmaMk neq
  ¬∑ let _ := (‚ä• : TopologicalSpace Œπ); have : DiscreteTopology Œπ := ‚ü®rfl‚ü©
    exact separated_by_continuous (continuous_def.2 fun u _ => isOpen_sigma_fst_preimage u) h
#align sigma.t2_space Sigma.t2Space

variable {Œ≥ : Type*} [TopologicalSpace Œ≤] [TopologicalSpace Œ≥]

theorem isClosed_eq [T2Space Œ±] {f g : Œ≤ ‚Üí Œ±} (hf : Continuous f) (hg : Continuous g) :
    IsClosed { x : Œ≤ | f x = g x } :=
  continuous_iff_isClosed.mp (hf.prod_mk hg) _ isClosed_diagonal
#align is_closed_eq isClosed_eq

theorem isOpen_ne_fun [T2Space Œ±] {f g : Œ≤ ‚Üí Œ±} (hf : Continuous f) (hg : Continuous g) :
    IsOpen { x : Œ≤ | f x ‚â† g x } :=
  isOpen_compl_iff.mpr <| isClosed_eq hf hg
#align is_open_ne_fun isOpen_ne_fun

/-- If two continuous maps are equal on `s`, then they are equal on the closure of `s`. See also
`Set.EqOn.of_subset_closure` for a more general version. -/
protected theorem Set.EqOn.closure [T2Space Œ±] {s : Set Œ≤} {f g : Œ≤ ‚Üí Œ±} (h : EqOn f g s)
    (hf : Continuous f) (hg : Continuous g) : EqOn f g (closure s) :=
  closure_minimal h (isClosed_eq hf hg)
#align set.eq_on.closure Set.EqOn.closure

/-- If two continuous functions are equal on a dense set, then they are equal. -/
theorem Continuous.ext_on [T2Space Œ±] {s : Set Œ≤} (hs : Dense s) {f g : Œ≤ ‚Üí Œ±} (hf : Continuous f)
    (hg : Continuous g) (h : EqOn f g s) : f = g :=
  funext fun x => h.closure hf hg (hs x)
#align continuous.ext_on Continuous.ext_on

theorem eqOn_closure‚ÇÇ' [T2Space Œ±] {s : Set Œ≤} {t : Set Œ≥} {f g : Œ≤ ‚Üí Œ≥ ‚Üí Œ±}
    (h : ‚àÄ x ‚àà s, ‚àÄ y ‚àà t, f x y = g x y) (hf‚ÇÅ : ‚àÄ x, Continuous (f x))
    (hf‚ÇÇ : ‚àÄ y, Continuous fun x => f x y) (hg‚ÇÅ : ‚àÄ x, Continuous (g x))
    (hg‚ÇÇ : ‚àÄ y, Continuous fun x => g x y) : ‚àÄ x ‚àà closure s, ‚àÄ y ‚àà closure t, f x y = g x y :=
  suffices closure s ‚äÜ ‚ãÇ y ‚àà closure t, { x | f x y = g x y } by simpa only [subset_def, mem_iInter]
  (closure_minimal fun x hx => mem_iInter‚ÇÇ.2 <| Set.EqOn.closure (h x hx) (hf‚ÇÅ _) (hg‚ÇÅ _)) <|
    isClosed_biInter fun y _ => isClosed_eq (hf‚ÇÇ _) (hg‚ÇÇ _)
#align eq_on_closure‚ÇÇ' eqOn_closure‚ÇÇ'

theorem eqOn_closure‚ÇÇ [T2Space Œ±] {s : Set Œ≤} {t : Set Œ≥} {f g : Œ≤ ‚Üí Œ≥ ‚Üí Œ±}
    (h : ‚àÄ x ‚àà s, ‚àÄ y ‚àà t, f x y = g x y) (hf : Continuous (uncurry f))
    (hg : Continuous (uncurry g)) : ‚àÄ x ‚àà closure s, ‚àÄ y ‚àà closure t, f x y = g x y :=
  eqOn_closure‚ÇÇ' h (fun x => continuous_uncurry_left x hf) (fun x => continuous_uncurry_right x hf)
    (fun y => continuous_uncurry_left y hg) fun y => continuous_uncurry_right y hg
#align eq_on_closure‚ÇÇ eqOn_closure‚ÇÇ

/-- If `f x = g x` for all `x ‚àà s` and `f`, `g` are continuous on `t`, `s ‚äÜ t ‚äÜ closure s`, then
`f x = g x` for all `x ‚àà t`. See also `Set.EqOn.closure`. -/
theorem Set.EqOn.of_subset_closure [T2Space Œ±] {s t : Set Œ≤} {f g : Œ≤ ‚Üí Œ±} (h : EqOn f g s)
    (hf : ContinuousOn f t) (hg : ContinuousOn g t) (hst : s ‚äÜ t) (hts : t ‚äÜ closure s) :
    EqOn f g t := by
  intro x hx
  have : (ùìù[s] x).NeBot := mem_closure_iff_clusterPt.mp (hts hx)
  exact
    tendsto_nhds_unique_of_eventuallyEq ((hf x hx).mono_left <| nhdsWithin_mono _ hst)
      ((hg x hx).mono_left <| nhdsWithin_mono _ hst) (h.eventuallyEq_of_mem self_mem_nhdsWithin)
#align set.eq_on.of_subset_closure Set.EqOn.of_subset_closure

theorem Function.LeftInverse.closed_range [T2Space Œ±] {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ±}
    (h : Function.LeftInverse f g) (hf : Continuous f) (hg : Continuous g) : IsClosed (range g) :=
  have : EqOn (g ‚àò f) id (closure <| range g) :=
    h.rightInvOn_range.eqOn.closure (hg.comp hf) continuous_id
  isClosed_of_closure_subset fun x hx => ‚ü®f x, this hx‚ü©
#align function.left_inverse.closed_range Function.LeftInverse.closed_range

theorem Function.LeftInverse.closedEmbedding [T2Space Œ±] {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ±}
    (h : Function.LeftInverse f g) (hf : Continuous f) (hg : Continuous g) : ClosedEmbedding g :=
  ‚ü®h.embedding hf hg, h.closed_range hf hg‚ü©
#align function.left_inverse.closed_embedding Function.LeftInverse.closedEmbedding

theorem isCompact_isCompact_separated [T2Space Œ±] {s t : Set Œ±} (hs : IsCompact s)
    (ht : IsCompact t) (hst : Disjoint s t) : SeparatedNhds s t := by
  simp only [SeparatedNhds, prod_subset_compl_diagonal_iff_disjoint.symm] at hst ‚ä¢
  exact generalized_tube_lemma hs ht isClosed_diagonal.isOpen_compl hst
#align is_compact_is_compact_separated isCompact_isCompact_separated

section SeparatedFinset

theorem finset_disjoint_finset_opens_of_t2 [T2Space Œ±] (s t : Finset Œ±) (h : Disjoint s t) :
    SeparatedNhds (s : Set Œ±) t :=
  isCompact_isCompact_separated s.finite_toSet.isCompact t.finite_toSet.isCompact <| by
    exact_mod_cast h
#align finset_disjoint_finset_opens_of_t2 finset_disjoint_finset_opens_of_t2

theorem point_disjoint_finset_opens_of_t2 [T2Space Œ±] {x : Œ±} {s : Finset Œ±} (h : x ‚àâ s) :
    SeparatedNhds ({x} : Set Œ±) s := by
  exact_mod_cast finset_disjoint_finset_opens_of_t2 {x} s (Finset.disjoint_singleton_left.mpr h)
#align point_disjoint_finset_opens_of_t2 point_disjoint_finset_opens_of_t2

end SeparatedFinset

/-- In a `T2Space`, every compact set is closed. -/
theorem IsCompact.isClosed [T2Space Œ±] {s : Set Œ±} (hs : IsCompact s) : IsClosed s :=
  isOpen_compl_iff.1 <| isOpen_iff_forall_mem_open.mpr fun x hx =>
    let ‚ü®u, v, _, vo, su, xv, uv‚ü© :=
      isCompact_isCompact_separated hs isCompact_singleton (disjoint_singleton_right.2 hx)
    ‚ü®v, (uv.mono_left <| show s ‚â§ u from su).subset_compl_left, vo, by simpa using xv‚ü©
#align is_compact.is_closed IsCompact.isClosed

@[simp]
theorem Filter.coclosedCompact_eq_cocompact [T2Space Œ±] : coclosedCompact Œ± = cocompact Œ± := by
  simp only [coclosedCompact, cocompact, iInf_and',
    and_iff_right_of_imp (@IsCompact.isClosed Œ± _ _ _)]
#align filter.coclosed_compact_eq_cocompact Filter.coclosedCompact_eq_cocompact

@[simp]
theorem Bornology.relativelyCompact_eq_inCompact [T2Space Œ±] :
    Bornology.relativelyCompact Œ± = Bornology.inCompact Œ± :=
  Bornology.ext _ _ Filter.coclosedCompact_eq_cocompact
#align bornology.relatively_compact_eq_in_compact Bornology.relativelyCompact_eq_inCompact

theorem IsCompact.preimage_continuous [CompactSpace Œ±] [T2Space Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ≤}
    (hs : IsCompact s) (hf : Continuous f) : IsCompact (f ‚Åª¬π' s) :=
  (hs.isClosed.preimage hf).isCompact

/-- If `V : Œπ ‚Üí Set Œ±` is a decreasing family of compact sets then any neighborhood of
`‚ãÇ i, V i` contains some `V i`. This is a version of `exists_subset_nhds_of_isCompact'` where we
don't need to assume each `V i` closed because it follows from compactness since `Œ±` is
assumed to be Hausdorff. -/
theorem exists_subset_nhds_of_isCompact [T2Space Œ±] {Œπ : Type*} [Nonempty Œπ] {V : Œπ ‚Üí Set Œ±}
    (hV : Directed (¬∑ ‚äá ¬∑) V) (hV_cpct : ‚àÄ i, IsCompact (V i)) {U : Set Œ±}
    (hU : ‚àÄ x ‚àà ‚ãÇ i, V i, U ‚àà ùìù x) : ‚àÉ i, V i ‚äÜ U :=
  exists_subset_nhds_of_isCompact' hV hV_cpct (fun i => (hV_cpct i).isClosed) hU
#align exists_subset_nhds_of_is_compact exists_subset_nhds_of_isCompact

theorem CompactExhaustion.isClosed [T2Space Œ±] (K : CompactExhaustion Œ±) (n : ‚Ñï) : IsClosed (K n) :=
  (K.isCompact n).isClosed
#align compact_exhaustion.is_closed CompactExhaustion.isClosed

theorem IsCompact.inter [T2Space Œ±] {s t : Set Œ±} (hs : IsCompact s) (ht : IsCompact t) :
    IsCompact (s ‚à© t) :=
  hs.inter_right <| ht.isClosed
#align is_compact.inter IsCompact.inter

theorem isCompact_closure_of_subset_compact [T2Space Œ±] {s t : Set Œ±} (ht : IsCompact t)
    (h : s ‚äÜ t) : IsCompact (closure s) :=
  ht.of_isClosed_subset isClosed_closure (closure_minimal h ht.isClosed)
#align is_compact_closure_of_subset_compact isCompact_closure_of_subset_compact

@[simp]
theorem exists_compact_superset_iff [T2Space Œ±] {s : Set Œ±} :
    (‚àÉ K, IsCompact K ‚àß s ‚äÜ K) ‚Üî IsCompact (closure s) :=
  ‚ü®fun ‚ü®_K, hK, hsK‚ü© => isCompact_closure_of_subset_compact hK hsK, fun h =>
    ‚ü®closure s, h, subset_closure‚ü©‚ü©
#align exists_compact_superset_iff exists_compact_superset_iff

theorem image_closure_of_isCompact [T2Space Œ≤] {s : Set Œ±} (hs : IsCompact (closure s)) {f : Œ± ‚Üí Œ≤}
    (hf : ContinuousOn f (closure s)) : f '' closure s = closure (f '' s) :=
  Subset.antisymm hf.image_closure <|
    closure_minimal (image_subset f subset_closure) (hs.image_of_continuousOn hf).isClosed
#align image_closure_of_is_compact image_closure_of_isCompact

/-- If a compact set is covered by two open sets, then we can cover it by two compact subsets. -/
theorem IsCompact.binary_compact_cover [T2Space Œ±] {K U V : Set Œ±} (hK : IsCompact K)
    (hU : IsOpen U) (hV : IsOpen V) (h2K : K ‚äÜ U ‚à™ V) :
    ‚àÉ K‚ÇÅ K‚ÇÇ : Set Œ±, IsCompact K‚ÇÅ ‚àß IsCompact K‚ÇÇ ‚àß K‚ÇÅ ‚äÜ U ‚àß K‚ÇÇ ‚äÜ V ‚àß K = K‚ÇÅ ‚à™ K‚ÇÇ := by
  obtain ‚ü®O‚ÇÅ, O‚ÇÇ, h1O‚ÇÅ, h1O‚ÇÇ, h2O‚ÇÅ, h2O‚ÇÇ, hO‚ü© :=
    isCompact_isCompact_separated (hK.diff hU) (hK.diff hV)
      (by rwa [disjoint_iff_inter_eq_empty, diff_inter_diff, diff_eq_empty])
  exact
    ‚ü®_, _, hK.diff h1O‚ÇÅ, hK.diff h1O‚ÇÇ, by rwa [diff_subset_comm], by rwa [diff_subset_comm], by
      rw [‚Üê diff_inter, hO.inter_eq, diff_empty]‚ü©
#align is_compact.binary_compact_cover IsCompact.binary_compact_cover

/-- A continuous map from a compact space to a Hausdorff space is a closed map. -/
protected theorem Continuous.isClosedMap [CompactSpace Œ±] [T2Space Œ≤] {f : Œ± ‚Üí Œ≤}
    (h : Continuous f) : IsClosedMap f := fun _s hs => (hs.isCompact.image h).isClosed
#align continuous.is_closed_map Continuous.isClosedMap

/-- A continuous injective map from a compact space to a Hausdorff space is a closed embedding. -/
theorem Continuous.closedEmbedding [CompactSpace Œ±] [T2Space Œ≤] {f : Œ± ‚Üí Œ≤} (h : Continuous f)
    (hf : Function.Injective f) : ClosedEmbedding f :=
  closedEmbedding_of_continuous_injective_closed h hf h.isClosedMap
#align continuous.closed_embedding Continuous.closedEmbedding

/-- A continuous surjective map from a compact space to a Hausdorff space is a quotient map. -/
theorem QuotientMap.of_surjective_continuous [CompactSpace Œ±] [T2Space Œ≤] {f : Œ± ‚Üí Œ≤}
    (hsurj : Surjective f) (hcont : Continuous f) : QuotientMap f :=
  hcont.isClosedMap.to_quotientMap hcont hsurj
#align quotient_map.of_surjective_continuous QuotientMap.of_surjective_continuous

section

open Finset Function

/-- For every finite open cover `U·µ¢` of a compact set, there exists a compact cover `K·µ¢ ‚äÜ U·µ¢`. -/
theorem IsCompact.finite_compact_cover [T2Space Œ±] {s : Set Œ±} (hs : IsCompact s) {Œπ} (t : Finset Œπ)
    (U : Œπ ‚Üí Set Œ±) (hU : ‚àÄ i ‚àà t, IsOpen (U i)) (hsC : s ‚äÜ ‚ãÉ i ‚àà t, U i) :
    ‚àÉ K : Œπ ‚Üí Set Œ±, (‚àÄ i, IsCompact (K i)) ‚àß (‚àÄ i, K i ‚äÜ U i) ‚àß s = ‚ãÉ i ‚àà t, K i := by
  induction' t using Finset.induction with x t hx ih generalizing U s
  ¬∑ refine' ‚ü®fun _ => ‚àÖ, fun _ => isCompact_empty, fun i => empty_subset _, _‚ü©
    simpa only [subset_empty_iff, Finset.not_mem_empty, iUnion_false, iUnion_empty] using hsC
  simp only [Finset.set_biUnion_insert] at hsC
  simp only [Finset.forall_mem_insert] at hU
  have hU' : ‚àÄ i ‚àà t, IsOpen (U i) := fun i hi => hU.2 i hi
  rcases hs.binary_compact_cover hU.1 (isOpen_biUnion hU') hsC with
    ‚ü®K‚ÇÅ, K‚ÇÇ, h1K‚ÇÅ, h1K‚ÇÇ, h2K‚ÇÅ, h2K‚ÇÇ, hK‚ü©
  rcases ih h1K‚ÇÇ U hU' h2K‚ÇÇ with ‚ü®K, h1K, h2K, h3K‚ü©
  refine' ‚ü®update K x K‚ÇÅ, _, _, _‚ü©
  ¬∑ intro i
    rcases eq_or_ne i x with rfl | hi
    ¬∑ simp only [update_same, h1K‚ÇÅ]
    ¬∑ simp only [update_noteq hi, h1K]
  ¬∑ intro i
    rcases eq_or_ne i x with rfl | hi
    ¬∑ simp only [update_same, h2K‚ÇÅ]
    ¬∑ simp only [update_noteq hi, h2K]
  ¬∑ simp only [set_biUnion_insert_update _ hx, hK, h3K]
#align is_compact.finite_compact_cover IsCompact.finite_compact_cover

end

-- see Note [lower instance priority]
/-- A weakly locally compact Hausdorff space is locally compact. -/
instance WeaklyLocallyCompactSpace.locallyCompactSpace [WeaklyLocallyCompactSpace Œ±] [T2Space Œ±] :
    LocallyCompactSpace Œ± :=
  ‚ü®fun x _n hn =>
    let ‚ü®_u, un, uo, xu‚ü© := mem_nhds_iff.mp hn
    let ‚ü®k, kc, kx‚ü© := exists_compact_mem_nhds x
    -- K is compact but not necessarily contained in N.
    -- K \ U is again compact and doesn't contain x, so
    -- we may find open sets V, W separating x from K \ U.
    -- Then K \ W is a compact neighborhood of x contained in U.
    let ‚ü®v, w, vo, wo, xv, kuw, vw‚ü© :=
      isCompact_isCompact_separated isCompact_singleton (kc.diff uo)
        (disjoint_singleton_left.2 fun h => h.2 xu)
    have wn : w·∂ú ‚àà ùìù x :=
      mem_nhds_iff.mpr ‚ü®v, vw.subset_compl_right, vo, singleton_subset_iff.mp xv‚ü©
    ‚ü®k \ w, Filter.inter_mem kx wn, Subset.trans (diff_subset_comm.mp kuw) un, kc.diff wo‚ü©‚ü©
#align locally_compact_of_compact_nhds WeaklyLocallyCompactSpace.locallyCompactSpace

@[deprecated WeaklyLocallyCompactSpace.locallyCompactSpace]
theorem locally_compact_of_compact [T2Space Œ±] [CompactSpace Œ±] :
    LocallyCompactSpace Œ± :=
  inferInstance
#align locally_compact_of_compact locally_compact_of_compact

/-- In a weakly locally compact T‚ÇÇ space,
every compact set has an open neighborhood with compact closure. -/
theorem exists_open_superset_and_isCompact_closure [WeaklyLocallyCompactSpace Œ±] [T2Space Œ±]
    {K : Set Œ±} (hK : IsCompact K) : ‚àÉ V, IsOpen V ‚àß K ‚äÜ V ‚àß IsCompact (closure V) := by
  rcases exists_compact_superset hK with ‚ü®K', hK', hKK'‚ü©
  exact ‚ü®interior K', isOpen_interior, hKK',
    isCompact_closure_of_subset_compact hK' interior_subset‚ü©
#align exists_open_superset_and_is_compact_closure exists_open_superset_and_isCompact_closure

/-- In a weakly locally compact T‚ÇÇ space,
every point has an open neighborhood with compact closure. -/
theorem exists_open_with_compact_closure [WeaklyLocallyCompactSpace Œ±] [T2Space Œ±] (x : Œ±) :
    ‚àÉ U : Set Œ±, IsOpen U ‚àß x ‚àà U ‚àß IsCompact (closure U) := by
  simpa only [singleton_subset_iff]
    using exists_open_superset_and_isCompact_closure isCompact_singleton
#align exists_open_with_compact_closure exists_open_with_compact_closure

/-- In a locally compact T‚ÇÇ space, given a compact set `K` inside an open set `U`, we can find an
open set `V` between these sets with compact closure: `K ‚äÜ V` and the closure of `V` is inside `U`.
-/
theorem exists_open_between_and_isCompact_closure [LocallyCompactSpace Œ±] [T2Space Œ±] {K U : Set Œ±}
    (hK : IsCompact K) (hU : IsOpen U) (hKU : K ‚äÜ U) :
    ‚àÉ V, IsOpen V ‚àß K ‚äÜ V ‚àß closure V ‚äÜ U ‚àß IsCompact (closure V) := by
  rcases exists_compact_between hK hU hKU with ‚ü®V, hV, hKV, hVU‚ü©
  exact
    ‚ü®interior V, isOpen_interior, hKV, (closure_minimal interior_subset hV.isClosed).trans hVU,
      isCompact_closure_of_subset_compact hV interior_subset‚ü©
#align exists_open_between_and_is_compact_closure exists_open_between_and_isCompact_closure

theorem isPreirreducible_iff_subsingleton [T2Space Œ±] {S : Set Œ±} :
    IsPreirreducible S ‚Üî S.Subsingleton := by
  refine' ‚ü®fun h x hx y hy => _, Set.Subsingleton.isPreirreducible‚ü©
  by_contra e
  obtain ‚ü®U, V, hU, hV, hxU, hyV, h'‚ü© := t2_separation e
  exact ((h U V hU hV ‚ü®x, hx, hxU‚ü© ‚ü®y, hy, hyV‚ü©).mono <| inter_subset_right _ _).not_disjoint h'
#align is_preirreducible_iff_subsingleton isPreirreducible_iff_subsingleton

-- todo: use `alias` + `attribute [protected]` once we get `attribute [protected]`
protected lemma IsPreirreducible.subsingleton [T2Space Œ±] {S : Set Œ±} (h : IsPreirreducible S) :
    S.Subsingleton :=
  isPreirreducible_iff_subsingleton.1 h
#align is_preirreducible.subsingleton IsPreirreducible.subsingleton

theorem isIrreducible_iff_singleton [T2Space Œ±] {S : Set Œ±} : IsIrreducible S ‚Üî ‚àÉ x, S = {x} := by
  rw [IsIrreducible, isPreirreducible_iff_subsingleton,
    exists_eq_singleton_iff_nonempty_subsingleton]
#align is_irreducible_iff_singleton isIrreducible_iff_singleton

/-- There does not exist a nontrivial preirreducible T‚ÇÇ space. -/
theorem not_preirreducible_nontrivial_t2 (Œ±) [TopologicalSpace Œ±] [PreirreducibleSpace Œ±]
    [Nontrivial Œ±] [T2Space Œ±] : False :=
  (PreirreducibleSpace.isPreirreducible_univ (Œ± := Œ±)).subsingleton.not_nontrivial nontrivial_univ
#align not_preirreducible_nontrivial_t2 not_preirreducible_nontrivial_t2

end Separation

section RegularSpace

/-- A topological space is called a *regular space* if for any closed set `s` and `a ‚àâ s`, there
exist disjoint open sets `U ‚äá s` and `V ‚àã a`. We formulate this condition in terms of `Disjoint`ness
of filters `ùìùÀ¢ s` and `ùìù a`. -/
@[mk_iff regularSpace_iff]
class RegularSpace (X : Type u) [TopologicalSpace X] : Prop where
  /-- If `a` is a point that does not belong to a closed set `s`, then `a` and `s` admit disjoint
  neighborhoods.  -/
  regular : ‚àÄ {s : Set X} {a}, IsClosed s ‚Üí a ‚àâ s ‚Üí Disjoint (ùìùÀ¢ s) (ùìù a)
#align regular_space RegularSpace

theorem regularSpace_TFAE (X : Type u) [ TopologicalSpace X ] :
    List.TFAE [RegularSpace X,
      ‚àÄ (s : Set X) a, a ‚àâ closure s ‚Üí Disjoint (ùìùÀ¢ s) (ùìù a),
      ‚àÄ (a : X) (s : Set X), Disjoint (ùìùÀ¢ s) (ùìù a) ‚Üî a ‚àâ closure s,
      ‚àÄ (a : X) (s : Set X), s ‚àà ùìù a ‚Üí ‚àÉ t ‚àà ùìù a, IsClosed t ‚àß t ‚äÜ s,
      ‚àÄ a : X, (ùìù a).lift' closure ‚â§ ùìù a,
      ‚àÄ a : X , (ùìù a).lift' closure = ùìù a] := by
  tfae_have 1 ‚Üî 5
  ¬∑ rw [regularSpace_iff, (@compl_surjective (Set X) _).forall, forall_swap]
    simp only [isClosed_compl_iff, mem_compl_iff, Classical.not_not, @and_comm (_ ‚àà _),
      (nhds_basis_opens _).lift'_closure.le_basis_iff (nhds_basis_opens _), and_imp,
      (nhds_basis_opens _).disjoint_iff_right, exists_prop, ‚Üê subset_interior_iff_mem_nhdsSet,
      interior_compl, compl_subset_compl]
  tfae_have 5 ‚Üí 6
  ¬∑ exact fun h a => (h a).antisymm (ùìù _).le_lift'_closure
  tfae_have 6 ‚Üí 4
  ¬∑ intro H a s hs
    rw [‚Üê H] at hs
    rcases (ùìù a).basis_sets.lift'_closure.mem_iff.mp hs with ‚ü®U, hU, hUs‚ü©
    exact ‚ü®closure U, mem_of_superset hU subset_closure, isClosed_closure, hUs‚ü©
  tfae_have 4 ‚Üí 2
  ¬∑ intro H s a ha
    have ha' : s·∂ú ‚àà ùìù a := by rwa [‚Üê mem_interior_iff_mem_nhds, interior_compl]
    rcases H _ _ ha' with ‚ü®U, hU, hUc, hUs‚ü©
    refine' disjoint_of_disjoint_of_mem disjoint_compl_left _ hU
    rwa [‚Üê subset_interior_iff_mem_nhdsSet, hUc.isOpen_compl.interior_eq, subset_compl_comm]
  tfae_have 2 ‚Üí 3
  ¬∑ refine' fun H a s => ‚ü®fun hd has => mem_closure_iff_nhds_neBot.mp has _, H s a‚ü©
    exact (hd.symm.mono_right <| @principal_le_nhdsSet _ _ s).eq_bot
  tfae_have 3 ‚Üí 1
  ¬∑ exact fun H => ‚ü®fun hs ha => (H _ _).mpr <| hs.closure_eq.symm ‚ñ∏ ha‚ü©
  tfae_finish
#align regular_space_tfae regularSpace_TFAE

theorem RegularSpace.ofLift'_closure (h : ‚àÄ a : Œ±, (ùìù a).lift' closure = ùìù a) : RegularSpace Œ± :=
  Iff.mpr ((regularSpace_TFAE Œ±).out 0 5) h
#align regular_space.of_lift'_closure RegularSpace.ofLift'_closure

theorem RegularSpace.ofBasis {Œπ : Œ± ‚Üí Sort*} {p : ‚àÄ a, Œπ a ‚Üí Prop} {s : ‚àÄ a, Œπ a ‚Üí Set Œ±}
    (h‚ÇÅ : ‚àÄ a, (ùìù a).HasBasis (p a) (s a)) (h‚ÇÇ : ‚àÄ a i, p a i ‚Üí IsClosed (s a i)) :
    RegularSpace Œ± :=
  RegularSpace.ofLift'_closure fun a => (h‚ÇÅ a).lift'_closure_eq_self (h‚ÇÇ a)
#align regular_space.of_basis RegularSpace.ofBasis

theorem RegularSpace.ofExistsMemNhdsIsClosedSubset
    (h : ‚àÄ (a : Œ±), ‚àÄ s ‚àà ùìù a, ‚àÉ t ‚àà ùìù a, IsClosed t ‚àß t ‚äÜ s) : RegularSpace Œ± :=
  Iff.mpr ((regularSpace_TFAE Œ±).out 0 3) h
#align regular_space.of_exists_mem_nhds_is_closed_subset RegularSpace.ofExistsMemNhdsIsClosedSubset

variable [RegularSpace Œ±] {a : Œ±} {s : Set Œ±}

theorem disjoint_nhdsSet_nhds : Disjoint (ùìùÀ¢ s) (ùìù a) ‚Üî a ‚àâ closure s := by
  have h := (regularSpace_TFAE Œ±).out 0 2
  exact h.mp ‚Äπ_‚Ä∫ _ _
#align disjoint_nhds_set_nhds disjoint_nhdsSet_nhds

theorem disjoint_nhds_nhdsSet : Disjoint (ùìù a) (ùìùÀ¢ s) ‚Üî a ‚àâ closure s :=
  disjoint_comm.trans disjoint_nhdsSet_nhds
#align disjoint_nhds_nhds_set disjoint_nhds_nhdsSet

theorem exists_mem_nhds_isClosed_subset {a : Œ±} {s : Set Œ±} (h : s ‚àà ùìù a) :
    ‚àÉ t ‚àà ùìù a, IsClosed t ‚àß t ‚äÜ s := by
  have h' := (regularSpace_TFAE Œ±).out 0 3
  exact h'.mp ‚Äπ_‚Ä∫ _ _ h
#align exists_mem_nhds_is_closed_subset exists_mem_nhds_isClosed_subset

theorem closed_nhds_basis (a : Œ±) : (ùìù a).HasBasis (fun s : Set Œ± => s ‚àà ùìù a ‚àß IsClosed s) id :=
  hasBasis_self.2 fun _ => exists_mem_nhds_isClosed_subset
#align closed_nhds_basis closed_nhds_basis

theorem lift'_nhds_closure (a : Œ±) : (ùìù a).lift' closure = ùìù a :=
  (closed_nhds_basis a).lift'_closure_eq_self fun _ => And.right
#align lift'_nhds_closure lift'_nhds_closure

theorem Filter.HasBasis.nhds_closure {Œπ : Sort*} {a : Œ±} {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí Set Œ±}
    (h : (ùìù a).HasBasis p s) : (ùìù a).HasBasis p fun i => closure (s i) :=
  lift'_nhds_closure a ‚ñ∏ h.lift'_closure
#align filter.has_basis.nhds_closure Filter.HasBasis.nhds_closure

theorem hasBasis_nhds_closure (a : Œ±) : (ùìù a).HasBasis (fun s => s ‚àà ùìù a) closure :=
  (ùìù a).basis_sets.nhds_closure
#align has_basis_nhds_closure hasBasis_nhds_closure

theorem hasBasis_opens_closure (a : Œ±) : (ùìù a).HasBasis (fun s => a ‚àà s ‚àß IsOpen s) closure :=
  (nhds_basis_opens a).nhds_closure
#align has_basis_opens_closure hasBasis_opens_closure

theorem TopologicalSpace.IsTopologicalBasis.nhds_basis_closure {B : Set (Set Œ±)}
    (hB : IsTopologicalBasis B) (a : Œ±) :
    (ùìù a).HasBasis (fun s : Set Œ± => a ‚àà s ‚àß s ‚àà B) closure := by
  simpa only [and_comm] using hB.nhds_hasBasis.nhds_closure
#align topological_space.is_topological_basis.nhds_basis_closure TopologicalSpace.IsTopologicalBasis.nhds_basis_closure

theorem TopologicalSpace.IsTopologicalBasis.exists_closure_subset {B : Set (Set Œ±)}
    (hB : IsTopologicalBasis B) {a : Œ±} {s : Set Œ±} (h : s ‚àà ùìù a) :
    ‚àÉ t ‚àà B, a ‚àà t ‚àß closure t ‚äÜ s := by
  simpa only [exists_prop, and_assoc] using hB.nhds_hasBasis.nhds_closure.mem_iff.mp h
#align topological_space.is_topological_basis.exists_closure_subset TopologicalSpace.IsTopologicalBasis.exists_closure_subset

theorem disjoint_nhds_nhds_iff_not_specializes {a b : Œ±} : Disjoint (ùìù a) (ùìù b) ‚Üî ¬¨a ‚§≥ b := by
  rw [‚Üê nhdsSet_singleton, disjoint_nhdsSet_nhds, specializes_iff_mem_closure]
#align disjoint_nhds_nhds_iff_not_specializes disjoint_nhds_nhds_iff_not_specializes

theorem specializes_comm {a b : Œ±} : a ‚§≥ b ‚Üî b ‚§≥ a := by
  simp only [‚Üê (disjoint_nhds_nhds_iff_not_specializes (Œ± := Œ±)).not_left, disjoint_comm]
#align specializes_comm specializes_comm

alias ‚ü®Specializes.symm, _‚ü© := specializes_comm
#align specializes.symm Specializes.symm

theorem specializes_iff_inseparable {a b : Œ±} : a ‚§≥ b ‚Üî Inseparable a b :=
  ‚ü®fun h => h.antisymm h.symm, le_of_eq‚ü©
#align specializes_iff_inseparable specializes_iff_inseparable

theorem isClosed_setOf_specializes : IsClosed { p : Œ± √ó Œ± | p.1 ‚§≥ p.2 } := by
  simp only [‚Üê isOpen_compl_iff, compl_setOf, ‚Üê disjoint_nhds_nhds_iff_not_specializes,
    isOpen_setOf_disjoint_nhds_nhds]
#align is_closed_set_of_specializes isClosed_setOf_specializes

theorem isClosed_setOf_inseparable : IsClosed { p : Œ± √ó Œ± | Inseparable p.1 p.2 } := by
  simp only [‚Üê specializes_iff_inseparable, isClosed_setOf_specializes]
#align is_closed_set_of_inseparable isClosed_setOf_inseparable

protected theorem Inducing.regularSpace [TopologicalSpace Œ≤] {f : Œ≤ ‚Üí Œ±} (hf : Inducing f) :
    RegularSpace Œ≤ :=
  RegularSpace.ofBasis
    (fun b => by rw [hf.nhds_eq_comap b]; exact (closed_nhds_basis _).comap _)
    fun b s hs => by exact hs.2.preimage hf.continuous
#align inducing.regular_space Inducing.regularSpace

theorem regularSpace_induced (f : Œ≤ ‚Üí Œ±) : @RegularSpace Œ≤ (induced f ‚Äπ_‚Ä∫) :=
  letI := induced f ‚Äπ_‚Ä∫
  Inducing.regularSpace ‚ü®rfl‚ü©
#align regular_space_induced regularSpace_induced

theorem regularSpace_sInf {X} {T : Set (TopologicalSpace X)} (h : ‚àÄ t ‚àà T, @RegularSpace X t) :
    @RegularSpace X (sInf T) := by
  let _ := sInf T
  have : ‚àÄ a, (ùìù a).HasBasis
      (fun If : Œ£I : Set T, I ‚Üí Set X =>
        If.1.Finite ‚àß ‚àÄ i : If.1, If.2 i ‚àà @nhds X i a ‚àß @IsClosed X i (If.2 i))
      fun If => ‚ãÇ i : If.1, If.snd i := by
    intro a
    rw [nhds_sInf, ‚Üê iInf_subtype'']
    exact hasBasis_iInf fun t : T => @closed_nhds_basis X t (h t t.2) a
  refine' RegularSpace.ofBasis this fun a If hIf => isClosed_iInter fun i => _
  exact (hIf.2 i).2.mono (sInf_le (i : T).2)
#align regular_space_Inf regularSpace_sInf

theorem regularSpace_iInf {Œπ X} {t : Œπ ‚Üí TopologicalSpace X} (h : ‚àÄ i, @RegularSpace X (t i)) :
    @RegularSpace X (iInf t) :=
  regularSpace_sInf <| forall_range_iff.mpr h
#align regular_space_infi regularSpace_iInf

theorem RegularSpace.inf {X} {t‚ÇÅ t‚ÇÇ : TopologicalSpace X} (h‚ÇÅ : @RegularSpace X t‚ÇÅ)
    (h‚ÇÇ : @RegularSpace X t‚ÇÇ) : @RegularSpace X (t‚ÇÅ ‚äì t‚ÇÇ) := by
  rw [inf_eq_iInf]
  exact regularSpace_iInf (Bool.forall_bool.2 ‚ü®h‚ÇÇ, h‚ÇÅ‚ü©)
#align regular_space.inf RegularSpace.inf

instance {p : Œ± ‚Üí Prop} : RegularSpace (Subtype p) :=
  embedding_subtype_val.toInducing.regularSpace

instance [TopologicalSpace Œ≤] [RegularSpace Œ≤] : RegularSpace (Œ± √ó Œ≤) :=
  (regularSpace_induced (@Prod.fst Œ± Œ≤)).inf (regularSpace_induced (@Prod.snd Œ± Œ≤))

instance {Œπ : Type*} {œÄ : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (œÄ i)] [‚àÄ i, RegularSpace (œÄ i)] :
    RegularSpace (‚àÄ i, œÄ i) :=
  regularSpace_iInf fun _ => regularSpace_induced _

end RegularSpace

section T3

/-- A T‚ÇÉ space is a T‚ÇÄ space which is a regular space. Any T‚ÇÉ space is a T‚ÇÅ space, a T‚ÇÇ space, and
a T‚ÇÇ.‚ÇÖ space.  -/
class T3Space (Œ± : Type u) [TopologicalSpace Œ±] extends T0Space Œ±, RegularSpace Œ± : Prop
#align t3_space T3Space

instance (priority := 90) [T0Space Œ±] [RegularSpace Œ±] : T3Space Œ± := ‚ü®‚ü©

-- see Note [lower instance priority]
instance (priority := 100) T3Space.t25Space [T3Space Œ±] : T25Space Œ± := by
  refine' ‚ü®fun x y hne => _‚ü©
  rw [lift'_nhds_closure, lift'_nhds_closure]
  have : x ‚àâ closure {y} ‚à® y ‚àâ closure {x} :=
    (t0Space_iff_or_not_mem_closure Œ±).mp inferInstance x y hne
  simp only [‚Üê disjoint_nhds_nhdsSet, nhdsSet_singleton] at this
  exact this.elim id fun h => h.symm
#align t3_space.t2_5_space T3Space.t25Space

protected theorem Embedding.t3Space [TopologicalSpace Œ≤] [T3Space Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : Embedding f) : T3Space Œ± :=
  { toT0Space := hf.t0Space
    toRegularSpace := hf.toInducing.regularSpace }
#align embedding.t3_space Embedding.t3Space

instance Subtype.t3Space [T3Space Œ±] {p : Œ± ‚Üí Prop} : T3Space (Subtype p) :=
  embedding_subtype_val.t3Space
#align subtype.t3_space Subtype.t3Space

instance [TopologicalSpace Œ≤] [T3Space Œ±] [T3Space Œ≤] : T3Space (Œ± √ó Œ≤) := ‚ü®‚ü©

instance {Œπ : Type*} {œÄ : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (œÄ i)] [‚àÄ i, T3Space (œÄ i)] :
    T3Space (‚àÄ i, œÄ i) := ‚ü®‚ü©

/-- Given two points `x ‚â† y`, we can find neighbourhoods `x ‚àà V‚ÇÅ ‚äÜ U‚ÇÅ` and `y ‚àà V‚ÇÇ ‚äÜ U‚ÇÇ`,
with the `V‚Çñ` closed and the `U‚Çñ` open, such that the `U‚Çñ` are disjoint. -/
theorem disjoint_nested_nhds [T3Space Œ±] {x y : Œ±} (h : x ‚â† y) :
    ‚àÉ U‚ÇÅ ‚àà ùìù x, ‚àÉ V‚ÇÅ ‚àà ùìù x, ‚àÉ U‚ÇÇ ‚àà ùìù y, ‚àÉ V‚ÇÇ ‚àà ùìù y,
      IsClosed V‚ÇÅ ‚àß IsClosed V‚ÇÇ ‚àß IsOpen U‚ÇÅ ‚àß IsOpen U‚ÇÇ ‚àß V‚ÇÅ ‚äÜ U‚ÇÅ ‚àß V‚ÇÇ ‚äÜ U‚ÇÇ ‚àß Disjoint U‚ÇÅ U‚ÇÇ := by
  rcases t2_separation h with ‚ü®U‚ÇÅ, U‚ÇÇ, U‚ÇÅ_op, U‚ÇÇ_op, x_in, y_in, H‚ü©
  rcases exists_mem_nhds_isClosed_subset (U‚ÇÅ_op.mem_nhds x_in) with ‚ü®V‚ÇÅ, V‚ÇÅ_in, V‚ÇÅ_closed, h‚ÇÅ‚ü©
  rcases exists_mem_nhds_isClosed_subset (U‚ÇÇ_op.mem_nhds y_in) with ‚ü®V‚ÇÇ, V‚ÇÇ_in, V‚ÇÇ_closed, h‚ÇÇ‚ü©
  exact ‚ü®U‚ÇÅ, mem_of_superset V‚ÇÅ_in h‚ÇÅ, V‚ÇÅ, V‚ÇÅ_in, U‚ÇÇ, mem_of_superset V‚ÇÇ_in h‚ÇÇ, V‚ÇÇ, V‚ÇÇ_in,
    V‚ÇÅ_closed, V‚ÇÇ_closed, U‚ÇÅ_op, U‚ÇÇ_op, h‚ÇÅ, h‚ÇÇ, H‚ü©
#align disjoint_nested_nhds disjoint_nested_nhds

open SeparationQuotient

/-- The `SeparationQuotient` of a regular space is a T‚ÇÉ space. -/
instance [RegularSpace Œ±] : T3Space (SeparationQuotient Œ±) where
  regular {s a} hs ha := by
    rcases surjective_mk a with ‚ü®a, rfl‚ü©
    rw [‚Üê disjoint_comap_iff surjective_mk, comap_mk_nhds_mk, comap_mk_nhdsSet]
    exact RegularSpace.regular (hs.preimage continuous_mk) ha

end T3

section NormalSpace

/-- A topological space is said to be a *normal space* if any two disjoint closed sets
have disjoint open neighborhoods. -/
class NormalSpace (X : Type u) [TopologicalSpace X] : Prop where
  /-- Two disjoint sets in a normal space admit disjoint neighbourhoods. -/
  normal : ‚àÄ s t : Set X, IsClosed s ‚Üí IsClosed t ‚Üí Disjoint s t ‚Üí SeparatedNhds s t

theorem normal_separation [NormalSpace Œ±] {s t : Set Œ±} (H1 : IsClosed s) (H2 : IsClosed t)
    (H3 : Disjoint s t) : SeparatedNhds s t :=
  NormalSpace.normal s t H1 H2 H3
#align normal_separation normal_separation

theorem disjoint_nhdsSet_nhdsSet [NormalSpace Œ±] {s t : Set Œ±} (hs : IsClosed s) (ht : IsClosed t)
    (hd : Disjoint s t) : Disjoint (ùìùÀ¢ s) (ùìùÀ¢ t) :=
  (normal_separation hs ht hd).disjoint_nhdsSet

theorem normal_exists_closure_subset [NormalSpace Œ±] {s t : Set Œ±} (hs : IsClosed s) (ht : IsOpen t)
    (hst : s ‚äÜ t) : ‚àÉ u, IsOpen u ‚àß s ‚äÜ u ‚àß closure u ‚äÜ t := by
  have : Disjoint s t·∂ú := Set.disjoint_left.mpr fun x hxs hxt => hxt (hst hxs)
  rcases normal_separation hs (isClosed_compl_iff.2 ht) this with
    ‚ü®s', t', hs', ht', hss', htt', hs't'‚ü©
  refine ‚ü®s', hs', hss', Subset.trans (closure_minimal ?_ (isClosed_compl_iff.2 ht'))
    (compl_subset_comm.1 htt')‚ü©
  exact fun x hxs hxt => hs't'.le_bot ‚ü®hxs, hxt‚ü©
#align normal_exists_closure_subset normal_exists_closure_subset

/-- If the codomain of a closed embedding is a normal space, then so is the domain. -/
protected theorem ClosedEmbedding.normalSpace [TopologicalSpace Œ≤] [NormalSpace Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : ClosedEmbedding f) : NormalSpace Œ± where
  normal s t hs ht hst := by
    have H : SeparatedNhds (f '' s) (f '' t) :=
      NormalSpace.normal (f '' s) (f '' t) (hf.isClosedMap s hs) (hf.isClosedMap t ht)
        (disjoint_image_of_injective hf.inj hst)
    exact (H.preimage hf.continuous).mono (subset_preimage_image _ _) (subset_preimage_image _ _)

/-- A regular topological space with second countable topology is a normal space. -/
instance (priority := 100) NormalSpace.of_regularSpace_secondCountableTopology
    [RegularSpace Œ±] [SecondCountableTopology Œ±] : NormalSpace Œ± := by
  have key : ‚àÄ {s t : Set Œ±}, IsClosed t ‚Üí Disjoint s t ‚Üí
    ‚àÉ U : Set (countableBasis Œ±), (s ‚äÜ ‚ãÉ u ‚àà U, ‚Üëu) ‚àß (‚àÄ u ‚àà U, Disjoint (closure ‚Üëu) t) ‚àß
      ‚àÄ n : ‚Ñï, IsClosed (‚ãÉ (u ‚àà U) (_ : Encodable.encode u ‚â§ n), closure (u : Set Œ±)) := by
    intro s t hc hd
    rw [disjoint_left] at hd
    have : ‚àÄ x ‚àà s, ‚àÉ U ‚àà countableBasis Œ±, x ‚àà U ‚àß Disjoint (closure U) t := by
      intro x hx
      rcases (isBasis_countableBasis Œ±).exists_closure_subset (hc.compl_mem_nhds (hd hx))
        with ‚ü®u, hu, hxu, hut‚ü©
      exact ‚ü®u, hu, hxu, disjoint_left.2 hut‚ü©
    choose! U hu hxu hd using this
    set V : s ‚Üí countableBasis Œ± := MapsTo.restrict _ _ _ hu
    refine' ‚ü®range V, _, forall_range_iff.2 <| Subtype.forall.2 hd, fun n => _‚ü©
    ¬∑ rw [biUnion_range]
      exact fun x hx => mem_iUnion.2 ‚ü®‚ü®x, hx‚ü©, hxu x hx‚ü©
    ¬∑ simp only [‚Üê iSup_eq_iUnion, iSup_and']
      exact (((finite_le_nat n).preimage_embedding (Encodable.encode' _)).subset <|
        inter_subset_right _ _).isClosed_biUnion fun u _ => isClosed_closure
  refine' { normal := fun s t hs ht hd => _ }
  rcases key ht hd with ‚ü®U, hsU, hUd, hUc‚ü©
  rcases key hs hd.symm with ‚ü®V, htV, hVd, hVc‚ü©
  refine ‚ü®‚ãÉ u ‚àà U, ‚Üëu \ ‚ãÉ (v ‚àà V) (_ : Encodable.encode v ‚â§ Encodable.encode u), closure ‚Üëv,
    ‚ãÉ v ‚àà V, ‚Üëv \ ‚ãÉ (u ‚àà U) (_ : Encodable.encode u ‚â§ Encodable.encode v), closure ‚Üëu,
    isOpen_biUnion fun u _ => (isOpen_of_mem_countableBasis u.2).sdiff (hVc _),
    isOpen_biUnion fun v _ => (isOpen_of_mem_countableBasis v.2).sdiff (hUc _),
    fun x hx => ?_, fun x hx => ?_, ?_‚ü©
  ¬∑ rcases mem_iUnion‚ÇÇ.1 (hsU hx) with ‚ü®u, huU, hxu‚ü©
    refine' mem_biUnion huU ‚ü®hxu, _‚ü©
    simp only [mem_iUnion]
    rintro ‚ü®v, hvV, -, hxv‚ü©
    exact (hVd v hvV).le_bot ‚ü®hxv, hx‚ü©
  ¬∑ rcases mem_iUnion‚ÇÇ.1 (htV hx) with ‚ü®v, hvV, hxv‚ü©
    refine' mem_biUnion hvV ‚ü®hxv, _‚ü©
    simp only [mem_iUnion]
    rintro ‚ü®u, huU, -, hxu‚ü©
    exact (hUd u huU).le_bot ‚ü®hxu, hx‚ü©
  ¬∑ simp only [disjoint_left, mem_iUnion, mem_diff, not_exists, not_and, not_forall, not_not]
    rintro a ‚ü®u, huU, hau, haV‚ü© v hvV hav
    cases' le_total (Encodable.encode u) (Encodable.encode v) with hle hle
    exacts [‚ü®u, huU, hle, subset_closure hau‚ü©, (haV _ hvV hle <| subset_closure hav).elim]
#align normal_space_of_t3_second_countable NormalSpace.of_regularSpace_secondCountableTopology

end NormalSpace

section Normality

/-- A T‚ÇÑ space is a normal T‚ÇÅ space. -/
class T4Space (Œ± : Type u) [TopologicalSpace Œ±] extends T1Space Œ±, NormalSpace Œ± : Prop
#align normal_space NormalSpace

instance (priority := 100) [T1Space Œ±] [NormalSpace Œ±] : T4Space Œ± := ‚ü®‚ü©

-- see Note [lower instance priority]
instance (priority := 100) T4Space.t3Space [T4Space Œ±] : T3Space Œ± where
  regular hs hxs := by simpa only [nhdsSet_singleton] using (normal_separation hs isClosed_singleton
    (disjoint_singleton_right.mpr hxs)).disjoint_nhdsSet
#align normal_space.t3_space T4Space.t3Space

instance (priority := 100) T4Space.of_compactSpace_t2Space [CompactSpace Œ±] [T2Space Œ±] :
    T4Space Œ± where
  normal _s _t hs ht := isCompact_isCompact_separated hs.isCompact ht.isCompact
#align normal_of_compact_t2 T4Space.of_compactSpace_t2Space

/-- If the codomain of a closed embedding is a T‚ÇÑ space, then so is the domain. -/
protected theorem ClosedEmbedding.t4Space [TopologicalSpace Œ≤] [T4Space Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : ClosedEmbedding f) : T4Space Œ± where
  toT1Space := hf.toEmbedding.t1Space
  toNormalSpace := hf.normalSpace
#align closed_embedding.normal_space ClosedEmbedding.t4Space

namespace SeparationQuotient

/-- The `SeparationQuotient` of a normal space is a normal space. -/
instance [NormalSpace Œ±] : NormalSpace (SeparationQuotient Œ±) where
  normal s t hs ht hd := separatedNhds_iff_disjoint.2 <| by
    rw [‚Üê disjoint_comap_iff surjective_mk, comap_mk_nhdsSet, comap_mk_nhdsSet]
    exact disjoint_nhdsSet_nhdsSet (hs.preimage continuous_mk) (ht.preimage continuous_mk)
      (hd.preimage mk)

end SeparationQuotient

variable (Œ±)

end Normality

section CompletelyNormal

/-- A topological space `Œ±` is a *completely normal Hausdorff space* if each subspace `s : Set Œ±` is
a normal Hausdorff space. Equivalently, `Œ±` is a `T‚ÇÅ` space and for any two sets `s`, `t` such that
`closure s` is disjoint with `t` and `s` is disjoint with `closure t`, there exist disjoint
neighbourhoods of `s` and `t`. -/
class T5Space (Œ± : Type u) [TopologicalSpace Œ±] extends T1Space Œ± : Prop where
  /-- If `closure s` is disjoint with `t` and `s` is disjoint with `closure t`, then `s` and `t`
  admit disjoint neighbourhoods. -/
  completely_normal :
    ‚àÄ ‚¶És t : Set Œ±‚¶Ñ, Disjoint (closure s) t ‚Üí Disjoint s (closure t) ‚Üí Disjoint (ùìùÀ¢ s) (ùìùÀ¢ t)
#align t5_space T5Space

export T5Space (completely_normal)

theorem Embedding.t5Space [TopologicalSpace Œ≤] [T5Space Œ≤] {e : Œ± ‚Üí Œ≤} (he : Embedding e) :
    T5Space Œ± := by
  haveI := he.t1Space
  refine' ‚ü®fun s t hd‚ÇÅ hd‚ÇÇ => _‚ü©
  simp only [he.toInducing.nhdsSet_eq_comap]
  refine disjoint_comap (completely_normal ?_ ?_)
  ¬∑ rwa [‚Üê subset_compl_iff_disjoint_left, image_subset_iff, preimage_compl,
      ‚Üê he.closure_eq_preimage_closure_image, subset_compl_iff_disjoint_left]
  ¬∑ rwa [‚Üê subset_compl_iff_disjoint_right, image_subset_iff, preimage_compl,
      ‚Üê he.closure_eq_preimage_closure_image, subset_compl_iff_disjoint_right]
#align embedding.t5_space Embedding.t5Space

/-- A subspace of a `T‚ÇÖ` space is a `T‚ÇÖ` space. -/
instance [T5Space Œ±] {p : Œ± ‚Üí Prop} : T5Space { x // p x } :=
  embedding_subtype_val.t5Space

-- see Note [lower instance priority]
/-- A `T‚ÇÖ` space is a `T‚ÇÑ` space. -/
instance (priority := 100) T5Space.toT4Space [T5Space Œ±] : T4Space Œ± where
  normal s t hs ht hd := separatedNhds_iff_disjoint.2 <|
    completely_normal (by rwa [hs.closure_eq]) (by rwa [ht.closure_eq])
#align t5_space.to_normal_space T5Space.toT4Space

open SeparationQuotient

/-- The `SeparationQuotient` of a completely normal R‚ÇÄ space is a T‚ÇÖ space.
We don't have typeclasses for completely normal spaces (without T‚ÇÅ assumption) and R‚ÇÄ spaces,
so we use `T5Space` for assumption and for conclusion.

One can prove this using a homeomorphism between `Œ±` and `SeparationQuotient Œ±`.
We give an alternative proof of the `completely_normal` axiom
that works without assuming that `Œ±` is a T‚ÇÅ space. -/
instance [T5Space Œ±] : T5Space (SeparationQuotient Œ±) where
  completely_normal s t hd‚ÇÅ hd‚ÇÇ := by
    rw [‚Üê disjoint_comap_iff surjective_mk, comap_mk_nhdsSet, comap_mk_nhdsSet]
    apply T5Space.completely_normal <;> rw [‚Üê preimage_mk_closure]
    exacts [hd‚ÇÅ.preimage mk, hd‚ÇÇ.preimage mk]

end CompletelyNormal

/-- In a compact t2 space, the connected component of a point equals the intersection of all
its clopen neighbourhoods. -/
theorem connectedComponent_eq_iInter_clopen [T2Space Œ±] [CompactSpace Œ±] (x : Œ±) :
    connectedComponent x = ‚ãÇ Z : { Z : Set Œ± // IsClopen Z ‚àß x ‚àà Z }, Z := by
  apply Subset.antisymm connectedComponent_subset_iInter_clopen
  -- Reduce to showing that the clopen intersection is connected.
  refine' IsPreconnected.subset_connectedComponent _ (mem_iInter.2 fun Z => Z.2.2)
  -- We do this by showing that any disjoint cover by two closed sets implies
  -- that one of these closed sets must contain our whole thing.
  -- To reduce to the case where the cover is disjoint on all of `Œ±` we need that `s` is closed
  have hs : @IsClosed Œ± _ (‚ãÇ Z : { Z : Set Œ± // IsClopen Z ‚àß x ‚àà Z }, Z) :=
    isClosed_iInter fun Z => Z.2.1.2
  rw [isPreconnected_iff_subset_of_fully_disjoint_closed hs]
  intro a b ha hb hab ab_disj
  -- Since our space is normal, we get two larger disjoint open sets containing the disjoint
  -- closed sets. If we can show that our intersection is a subset of any of these we can then
  -- "descend" this to show that it is a subset of either a or b.
  rcases normal_separation ha hb ab_disj with ‚ü®u, v, hu, hv, hau, hbv, huv‚ü©
  obtain ‚ü®Z, H‚ü© : ‚àÉ Z : Set Œ±, IsClopen Z ‚àß x ‚àà Z ‚àß Z ‚äÜ u ‚à™ v
  /- Now we find a clopen set `Z` around `x`, contained in `u ‚à™ v`. We utilize the fact that
  `X \ u ‚à™ v` will be compact, so there must be some finite intersection of clopen neighbourhoods of
  `X` disjoint to it, but a finite intersection of clopen sets is clopen so we let this be our
  `Z`. -/
  ¬∑ have H1 := (hu.union hv).isClosed_compl.isCompact.inter_iInter_nonempty
      (fun Z : { Z : Set Œ± // IsClopen Z ‚àß x ‚àà Z } => Z) fun Z => Z.2.1.2
    rw [‚Üê not_disjoint_iff_nonempty_inter, imp_not_comm, not_forall] at H1
    cases' H1 (disjoint_compl_left_iff_subset.2 <| hab.trans <| union_subset_union hau hbv)
      with Zi H2
    refine' ‚ü®‚ãÇ U ‚àà Zi, Subtype.val U, _, _, _‚ü©
    ¬∑ exact isClopen_biInter_finset fun Z _ => Z.2.1
    ¬∑ exact mem_iInter‚ÇÇ.2 fun Z _ => Z.2.2
    ¬∑ rwa [‚Üê disjoint_compl_left_iff_subset, disjoint_iff_inter_eq_empty,
        ‚Üê not_nonempty_iff_eq_empty]
  -- So, we get a disjoint decomposition `Z = Z ‚à© u ‚à™ Z ‚à© v` of clopen sets. The intersection of all
  -- clopen neighbourhoods will then lie in whichever of u or v x lies in and hence will be a subset
  -- of either a or b.
  ¬∑ have H1 := isClopen_inter_of_disjoint_cover_clopen H.1 H.2.2 hu hv huv
    rw [union_comm] at H
    have H2 := isClopen_inter_of_disjoint_cover_clopen H.1 H.2.2 hv hu huv.symm
    by_cases hxu : x ‚àà u <;> [left; right]
    -- The x ‚àà u case.
    ¬∑ suffices ‚ãÇ Z : { Z : Set Œ± // IsClopen Z ‚àß x ‚àà Z }, ‚ÜëZ ‚äÜ u
        from Disjoint.left_le_of_le_sup_right hab (huv.mono this hbv)
      ¬∑ apply Subset.trans _ (inter_subset_right Z u)
        exact iInter_subset (fun Z : { Z : Set Œ± // IsClopen Z ‚àß x ‚àà Z } => Z.1)
          ‚ü®Z ‚à© u, H1, mem_inter H.2.1 hxu‚ü©
    -- If x ‚àâ u, we get x ‚àà v since x ‚àà u ‚à™ v. The rest is then like the x ‚àà u case.
    ¬∑ have h1 : x ‚àà v :=
        (hab.trans (union_subset_union hau hbv) (mem_iInter.2 fun i => i.2.2)).resolve_left hxu
      suffices ‚ãÇ Z : { Z : Set Œ± // IsClopen Z ‚àß x ‚àà Z }, ‚ÜëZ ‚äÜ v
        from (huv.symm.mono this hau).left_le_of_le_sup_left hab
      ¬∑ refine Subset.trans ?_ (inter_subset_right Z v)
        exact iInter_subset (fun Z : { Z : Set Œ± // IsClopen Z ‚àß x ‚àà Z } => Z.1)
          ‚ü®Z ‚à© v, H2, mem_inter H.2.1 h1‚ü©
#align connected_component_eq_Inter_clopen connectedComponent_eq_iInter_clopen

section Profinite

/-- A T1 space with a clopen basis is totally separated. -/
theorem totallySeparatedSpace_of_t1_of_basis_clopen [T1Space Œ±]
    (h : IsTopologicalBasis { s : Set Œ± | IsClopen s }) : TotallySeparatedSpace Œ± := by
  constructor
  rintro x - y - hxy
  rcases h.mem_nhds_iff.mp (isOpen_ne.mem_nhds hxy) with ‚ü®U, hU, hxU, hyU‚ü©
  exact ‚ü®U, U·∂ú, hU.isOpen, hU.compl.isOpen, hxU, fun h => hyU h rfl, (union_compl_self U).superset,
    disjoint_compl_right‚ü©
#align totally_separated_space_of_t1_of_basis_clopen totallySeparatedSpace_of_t1_of_basis_clopen

variable [T2Space Œ±] [CompactSpace Œ±]

/-- A compact Hausdorff space is totally disconnected if and only if it is totally separated, this
  is also true for locally compact spaces. -/
theorem compact_t2_tot_disc_iff_tot_sep : TotallyDisconnectedSpace Œ± ‚Üî TotallySeparatedSpace Œ± := by
  refine ‚ü®fun h => ‚ü®fun x _ y _ => ?_‚ü©, @TotallySeparatedSpace.totallyDisconnectedSpace _ _‚ü©
  contrapose!
  intro hyp
  suffices x ‚àà connectedComponent y by
    simpa [totallyDisconnectedSpace_iff_connectedComponent_singleton.1 h y, mem_singleton_iff]
  rw [connectedComponent_eq_iInter_clopen, mem_iInter]
  rintro ‚ü®w : Set Œ±, hw : IsClopen w, hy : y ‚àà w‚ü©
  by_contra hx
  exact hyp w·∂ú w hw.2.isOpen_compl hw.1 hx hy (@isCompl_compl _ w _).symm.codisjoint.top_le
    disjoint_compl_left
#align compact_t2_tot_disc_iff_tot_sep compact_t2_tot_disc_iff_tot_sep

variable [TotallyDisconnectedSpace Œ±]

/-- A totally disconnected compact Hausdorff space is totally separated. -/
instance : TotallySeparatedSpace Œ± := compact_t2_tot_disc_iff_tot_sep.mp inferInstance

theorem nhds_basis_clopen (x : Œ±) : (ùìù x).HasBasis (fun s : Set Œ± => x ‚àà s ‚àß IsClopen s) id :=
  ‚ü®fun U => by
    constructor
    ¬∑ have hx : connectedComponent x = {x} :=
        totallyDisconnectedSpace_iff_connectedComponent_singleton.mp ‚Äπ_‚Ä∫ x
      rw [connectedComponent_eq_iInter_clopen] at hx
      intro hU
      let N := { Z // IsClopen Z ‚àß x ‚àà Z }
      suffices : ‚àÉ Z : N, Z.val ‚äÜ U
      ¬∑ rcases this with ‚ü®‚ü®s, hs, hs'‚ü©, hs''‚ü©; exact ‚ü®s, ‚ü®hs', hs‚ü©, hs''‚ü©
      haveI : Nonempty N := ‚ü®‚ü®univ, isClopen_univ, mem_univ x‚ü©‚ü©
      have hNcl : ‚àÄ Z : N, IsClosed Z.val := fun Z => Z.property.1.2
      have hdir : Directed Superset fun Z : N => Z.val := by
        rintro ‚ü®s, hs, hxs‚ü© ‚ü®t, ht, hxt‚ü©
        exact ‚ü®‚ü®s ‚à© t, hs.inter ht, ‚ü®hxs, hxt‚ü©‚ü©, inter_subset_left s t, inter_subset_right s t‚ü©
      have h_nhd : ‚àÄ y ‚àà ‚ãÇ Z : N, Z.val, U ‚àà ùìù y := fun y y_in => by
        erw [hx, mem_singleton_iff] at y_in
        rwa [y_in]
      exact exists_subset_nhds_of_compactSpace hdir hNcl h_nhd
    ¬∑ rintro ‚ü®V, ‚ü®hxV, V_op, -‚ü©, hUV : V ‚äÜ U‚ü©
      rw [mem_nhds_iff]
      exact ‚ü®V, hUV, V_op, hxV‚ü©‚ü©
#align nhds_basis_clopen nhds_basis_clopen

theorem isTopologicalBasis_clopen : IsTopologicalBasis { s : Set Œ± | IsClopen s } := by
  apply isTopologicalBasis_of_open_of_nhds fun U (hU : IsClopen U) => hU.1
  intro x U hxU U_op
  have : U ‚àà ùìù x := IsOpen.mem_nhds U_op hxU
  rcases (nhds_basis_clopen x).mem_iff.mp this with ‚ü®V, ‚ü®hxV, hV‚ü©, hVU : V ‚äÜ U‚ü©
  use V
  tauto
#align is_topological_basis_clopen isTopologicalBasis_clopen

/-- Every member of an open set in a compact Hausdorff totally disconnected space
  is contained in a clopen set contained in the open set.  -/
theorem compact_exists_clopen_in_open {x : Œ±} {U : Set Œ±} (is_open : IsOpen U) (memU : x ‚àà U) :
    ‚àÉ V : Set Œ±, IsClopen V ‚àß x ‚àà V ‚àß V ‚äÜ U :=
  isTopologicalBasis_clopen.mem_nhds_iff.1 (is_open.mem_nhds memU)
#align compact_exists_clopen_in_open compact_exists_clopen_in_open

end Profinite

section LocallyCompact

variable {H : Type*} [TopologicalSpace H] [LocallyCompactSpace H] [T2Space H]

/-- A locally compact Hausdorff totally disconnected space has a basis with clopen elements. -/
theorem loc_compact_Haus_tot_disc_of_zero_dim [TotallyDisconnectedSpace H] :
    IsTopologicalBasis { s : Set H | IsClopen s } := by
  refine isTopologicalBasis_of_open_of_nhds (fun u hu => hu.1) fun x U memU hU => ?_
  obtain ‚ü®s, comp, xs, sU‚ü© := exists_compact_subset hU memU
  let u : Set s := ((‚Üë) : s ‚Üí H) ‚Åª¬π' interior s
  have u_open_in_s : IsOpen u := isOpen_interior.preimage continuous_subtype_val
  lift x to s using interior_subset xs
  haveI : CompactSpace s := isCompact_iff_compactSpace.1 comp
  obtain ‚ü®V : Set s, clopen_in_s, Vx, V_sub‚ü© := compact_exists_clopen_in_open u_open_in_s xs
  have V_clopen : IsClopen (((‚Üë) : s ‚Üí H) '' V) := by
    refine' ‚ü®_, comp.isClosed.closedEmbedding_subtype_val.closed_iff_image_closed.1 clopen_in_s.2‚ü©
    let v : Set u := ((‚Üë) : u ‚Üí s) ‚Åª¬π' V
    have : ((‚Üë) : u ‚Üí H) = ((‚Üë) : s ‚Üí H) ‚àò ((‚Üë) : u ‚Üí s) := rfl
    have f0 : Embedding ((‚Üë) : u ‚Üí H) := embedding_subtype_val.comp embedding_subtype_val
    have f1 : OpenEmbedding ((‚Üë) : u ‚Üí H) := by
      refine' ‚ü®f0, _‚ü©
      ¬∑ have : Set.range ((‚Üë) : u ‚Üí H) = interior s := by
          rw [this, Set.range_comp, Subtype.range_coe, Subtype.image_preimage_coe]
          apply Set.inter_eq_self_of_subset_left interior_subset
        rw [this]
        apply isOpen_interior
    have f2 : IsOpen v := clopen_in_s.1.preimage continuous_subtype_val
    have f3 : ((‚Üë) : s ‚Üí H) '' V = ((‚Üë) : u ‚Üí H) '' v := by
      rw [this, image_comp, Subtype.image_preimage_coe, inter_eq_self_of_subset_left V_sub]
    rw [f3]
    apply f1.isOpenMap v f2
  refine' ‚ü®(‚Üë) '' V, V_clopen, by simp [Vx], Subset.trans _ sU‚ü©
  simp
set_option linter.uppercaseLean3 false in
#align loc_compact_Haus_tot_disc_of_zero_dim loc_compact_Haus_tot_disc_of_zero_dim

/-- A locally compact Hausdorff space is totally disconnected
  if and only if it is totally separated. -/
theorem loc_compact_t2_tot_disc_iff_tot_sep :
    TotallyDisconnectedSpace H ‚Üî TotallySeparatedSpace H := by
  constructor
  ¬∑ intro h
    exact totallySeparatedSpace_of_t1_of_basis_clopen loc_compact_Haus_tot_disc_of_zero_dim
  apply TotallySeparatedSpace.totallyDisconnectedSpace
#align loc_compact_t2_tot_disc_iff_tot_sep loc_compact_t2_tot_disc_iff_tot_sep

end LocallyCompact

/-- `ConnectedComponents Œ±` is Hausdorff when `Œ±` is Hausdorff and compact -/
instance ConnectedComponents.t2 [T2Space Œ±] [CompactSpace Œ±] : T2Space (ConnectedComponents Œ±) := by
  -- Proof follows that of: https://stacks.math.columbia.edu/tag/0900
  -- Fix 2 distinct connected components, with points a and b
  refine ‚ü®ConnectedComponents.surjective_coe.forall‚ÇÇ.2 fun a b ne => ?_‚ü©
  rw [ConnectedComponents.coe_ne_coe] at ne
  have h := connectedComponent_disjoint ne
  -- write ‚Üëb as the intersection of all clopen subsets containing it
  rw [connectedComponent_eq_iInter_clopen b, disjoint_iff_inter_eq_empty] at h
  -- Now we show that this can be reduced to some clopen containing `‚Üëb` being disjoint to `‚Üëa`
  obtain ‚ü®U, V, hU, ha, hb, rfl‚ü© : ‚àÉ (U : Set Œ±) (V : Set (ConnectedComponents Œ±)),
      IsClopen U ‚àß connectedComponent a ‚à© U = ‚àÖ ‚àß connectedComponent b ‚äÜ U ‚àß (‚Üë) ‚Åª¬π' V = U := by
    have h :=
      (isClosed_connectedComponent (Œ± := Œ±)).isCompact.elim_finite_subfamily_closed
        _ (fun Z : { Z : Set Œ± // IsClopen Z ‚àß b ‚àà Z } => Z.2.1.2) h
    cases' h with fin_a ha
    -- This clopen and its complement will separate the connected components of `a` and `b`
    set U : Set Œ± := ‚ãÇ (i : { Z // IsClopen Z ‚àß b ‚àà Z }) (_ : i ‚àà fin_a), i
    have hU : IsClopen U := isClopen_biInter_finset fun i _ => i.2.1
    exact ‚ü®U, (‚Üë) '' U, hU, ha, subset_iInter‚ÇÇ fun Z _ => Z.2.1.connectedComponent_subset Z.2.2,
      (connectedComponents_preimage_image U).symm ‚ñ∏ hU.biUnion_connectedComponent_eq‚ü©
  rw [ConnectedComponents.quotientMap_coe.isClopen_preimage] at hU
  refine' ‚ü®V·∂ú, V, hU.compl.isOpen, hU.isOpen, _, hb mem_connectedComponent, disjoint_compl_left‚ü©
  exact fun h => flip Set.Nonempty.ne_empty ha ‚ü®a, mem_connectedComponent, h‚ü©
#align connected_components.t2 ConnectedComponents.t2
