/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
import Mathlib.Topology.UniformSpace.Basic
import Mathlib.Topology.Separation
import Mathlib.Order.Filter.CountableInter

#align_import topology.G_delta from "leanprover-community/mathlib"@"b9e46fe101fc897fb2e7edaf0bf1f09ea49eb81a"

/-!
# `GÎ´` sets

In this file we define `GÎ´` sets and prove their basic properties.

## Main definitions

* `IsGÎ´`: a set `s` is a `GÎ´` set if it can be represented as an intersection
  of countably many open sets;

* `residual`: the Ïƒ-filter of residual sets. A set `s` is called *residual* if it includes a
  countable intersection of dense open sets.

* `IsNowhereDense`: a set is called *nowhere dense* iff its closure has empty interior
* `IsMeagre`: a set `s` is called *meagre* iff its complement is residual

## Main results

We prove that finite or countable intersections of GÎ´ sets are GÎ´ sets. We also prove that the
continuity set of a function from a topological space to an (e)metric space is a GÎ´ set.

- `isClosed_isNowhereDense_iff_compl`: a closed set is nowhere dense iff
its complement is open and dense
- `isMeagre_iff_countable_union_isNowhereDense`: a set is meagre iff it is contained in a countable
union of nowhere dense sets
- subsets of meagre sets are meagre; countable unions of meagre sets are meagre

## Tags

GÎ´ set, residual set, nowhere dense set, meagre set
-/


noncomputable section

open Topology TopologicalSpace Filter Encodable Set
open scoped Uniformity

variable {X Y Î¹ : Type*} {Î¹' : Sort*}

set_option linter.uppercaseLean3 false

section IsGÎ´

variable [TopologicalSpace X]

/-- A GÎ´ set is a countable intersection of open sets. -/
def IsGÎ´ (s : Set X) : Prop :=
  âˆƒ T : Set (Set X), (âˆ€ t âˆˆ T, IsOpen t) âˆ§ T.Countable âˆ§ s = â‹‚â‚€ T
#align is_GÎ´ IsGÎ´

/-- An open set is a GÎ´ set. -/
theorem IsOpen.isGÎ´ {s : Set X} (h : IsOpen s) : IsGÎ´ s :=
  âŸ¨{s}, by simp [h], countable_singleton _, (Set.sInter_singleton _).symmâŸ©
#align is_open.is_GÎ´ IsOpen.isGÎ´

@[simp]
theorem isGÎ´_empty : IsGÎ´ (âˆ… : Set X) :=
  isOpen_empty.isGÎ´
#align is_GÎ´_empty isGÎ´_empty

@[simp]
theorem isGÎ´_univ : IsGÎ´ (univ : Set X) :=
  isOpen_univ.isGÎ´
#align is_GÎ´_univ isGÎ´_univ

theorem isGÎ´_biInter_of_isOpen {I : Set Î¹} (hI : I.Countable) {f : Î¹ â†’ Set X}
    (hf : âˆ€ i âˆˆ I, IsOpen (f i)) : IsGÎ´ (â‹‚ i âˆˆ I, f i) :=
  âŸ¨f '' I, by rwa [ball_image_iff], hI.image _, by rw [sInter_image]âŸ©
#align is_GÎ´_bInter_of_open isGÎ´_biInter_of_isOpen

theorem isGÎ´_iInter_of_isOpen [Countable Î¹'] {f : Î¹' â†’ Set X} (hf : âˆ€ i, IsOpen (f i)) :
    IsGÎ´ (â‹‚ i, f i) :=
  âŸ¨range f, by rwa [forall_range_iff], countable_range _, by rw [sInter_range]âŸ©
#align is_GÎ´_Inter_of_open isGÎ´_iInter_of_isOpen

lemma isGÎ´_iff_eq_iInter_nat {s : Set X} :
    IsGÎ´ s â†” âˆƒ (f : â„• â†’ Set X), (âˆ€ n, IsOpen (f n)) âˆ§ s = â‹‚ n, f n := by
  refine âŸ¨?_, ?_âŸ©
  Â· rintro âŸ¨T, hT, T_count, rflâŸ©
    rcases Set.eq_empty_or_nonempty T with rfl|hT
    Â· exact âŸ¨fun _n â†¦ univ, fun _n â†¦ isOpen_univ, by simpâŸ©
    Â· obtain âŸ¨f, hfâŸ© : âˆƒ (f : â„• â†’ Set X), T = range f := Countable.exists_eq_range T_count hT
      exact âŸ¨f, by aesop, by simp [hf]âŸ©
  Â· rintro âŸ¨f, hf, rflâŸ©
    apply isGÎ´_iInter_of_isOpen hf

/-- The intersection of an encodable family of GÎ´ sets is a GÎ´ set. -/
theorem isGÎ´_iInter [Countable Î¹'] {s : Î¹' â†’ Set X} (hs : âˆ€ i, IsGÎ´ (s i)) : IsGÎ´ (â‹‚ i, s i) := by
  choose T hTo hTc hTs using hs
  obtain rfl : s = fun i => â‹‚â‚€ T i := funext hTs
  refine' âŸ¨â‹ƒ i, T i, _, countable_iUnion hTc, (sInter_iUnion _).symmâŸ©
  simpa [@forall_swap Î¹'] using hTo
#align is_GÎ´_Inter isGÎ´_iInter

theorem isGÎ´_biInter {s : Set Î¹} (hs : s.Countable) {t : âˆ€ i âˆˆ s, Set X}
    (ht : âˆ€ (i) (hi : i âˆˆ s), IsGÎ´ (t i hi)) : IsGÎ´ (â‹‚ i âˆˆ s, t i â€¹_â€º) := by
  rw [biInter_eq_iInter]
  haveI := hs.toEncodable
  exact isGÎ´_iInter fun x => ht x x.2
#align is_GÎ´_bInter isGÎ´_biInter

/-- A countable intersection of GÎ´ sets is a GÎ´ set. -/
theorem isGÎ´_sInter {S : Set (Set X)} (h : âˆ€ s âˆˆ S, IsGÎ´ s) (hS : S.Countable) : IsGÎ´ (â‹‚â‚€ S) := by
  simpa only [sInter_eq_biInter] using isGÎ´_biInter hS h
#align is_GÎ´_sInter isGÎ´_sInter

theorem IsGÎ´.inter {s t : Set X} (hs : IsGÎ´ s) (ht : IsGÎ´ t) : IsGÎ´ (s âˆ© t) := by
  rw [inter_eq_iInter]
  exact isGÎ´_iInter (Bool.forall_bool.2 âŸ¨ht, hsâŸ©)
#align is_GÎ´.inter IsGÎ´.inter

/-- The union of two GÎ´ sets is a GÎ´ set. -/
theorem IsGÎ´.union {s t : Set X} (hs : IsGÎ´ s) (ht : IsGÎ´ t) : IsGÎ´ (s âˆª t) := by
  rcases hs with âŸ¨S, Sopen, Scount, rflâŸ©
  rcases ht with âŸ¨T, Topen, Tcount, rflâŸ©
  rw [sInter_union_sInter]
  apply isGÎ´_biInter_of_isOpen (Scount.prod Tcount)
  rintro âŸ¨a, bâŸ© âŸ¨ha, hbâŸ©
  exact (Sopen a ha).union (Topen b hb)
#align is_GÎ´.union IsGÎ´.union

-- porting note: TODO: add `iUnion` and `sUnion` versions
/-- The union of finitely many GÎ´ sets is a GÎ´ set. -/
theorem isGÎ´_biUnion {s : Set Î¹} (hs : s.Finite) {f : Î¹ â†’ Set X} (h : âˆ€ i âˆˆ s, IsGÎ´ (f i)) :
    IsGÎ´ (â‹ƒ i âˆˆ s, f i) := by
  refine' Finite.induction_on hs (by simp) _ h
  simp only [ball_insert_iff, biUnion_insert]
  exact fun _ _ ihs H => H.1.union (ihs H.2)
#align is_GÎ´_bUnion isGÎ´_biUnion

theorem IsClosed.isGÎ´ {X} [UniformSpace X] [IsCountablyGenerated (ð“¤ X)] {s : Set X}
    (hs : IsClosed s) : IsGÎ´ s := by
  rcases (@uniformity_hasBasis_open X _).exists_antitone_subbasis with âŸ¨U, hUo, hU, -âŸ©
  rw [â† hs.closure_eq, â† hU.biInter_biUnion_ball]
  refine' isGÎ´_biInter (to_countable _) fun n _ => IsOpen.isGÎ´ _
  exact isOpen_biUnion fun x _ => UniformSpace.isOpen_ball _ (hUo _).2
#align is_closed.is_GÎ´ IsClosed.isGÎ´

section T1Space

variable [T1Space X]

theorem isGÎ´_compl_singleton (x : X) : IsGÎ´ ({x}á¶œ : Set X) :=
  isOpen_compl_singleton.isGÎ´
#align is_GÎ´_compl_singleton isGÎ´_compl_singleton

theorem Set.Countable.isGÎ´_compl {s : Set X} (hs : s.Countable) : IsGÎ´ sá¶œ := by
  rw [â† biUnion_of_singleton s, compl_iUnionâ‚‚]
  exact isGÎ´_biInter hs fun x _ => isGÎ´_compl_singleton x
#align set.countable.is_GÎ´_compl Set.Countable.isGÎ´_compl

theorem Set.Finite.isGÎ´_compl {s : Set X} (hs : s.Finite) : IsGÎ´ sá¶œ :=
  hs.countable.isGÎ´_compl
#align set.finite.is_GÎ´_compl Set.Finite.isGÎ´_compl

theorem Set.Subsingleton.isGÎ´_compl {s : Set X} (hs : s.Subsingleton) : IsGÎ´ sá¶œ :=
  hs.finite.isGÎ´_compl
#align set.subsingleton.is_GÎ´_compl Set.Subsingleton.isGÎ´_compl

theorem Finset.isGÎ´_compl (s : Finset X) : IsGÎ´ (sá¶œ : Set X) :=
  s.finite_toSet.isGÎ´_compl
#align finset.is_GÎ´_compl Finset.isGÎ´_compl

variable [FirstCountableTopology X]

theorem isGÎ´_singleton (x : X) : IsGÎ´ ({x} : Set X) := by
  rcases (nhds_basis_opens x).exists_antitone_subbasis with âŸ¨U, hU, h_basisâŸ©
  rw [â† biInter_basis_nhds h_basis.toHasBasis]
  exact isGÎ´_biInter (to_countable _) fun n _ => (hU n).2.isGÎ´
#align is_GÎ´_singleton isGÎ´_singleton

theorem Set.Finite.isGÎ´ {s : Set X} (hs : s.Finite) : IsGÎ´ s :=
  Finite.induction_on hs isGÎ´_empty fun _ _ hs => (isGÎ´_singleton _).union hs
#align set.finite.is_GÎ´ Set.Finite.isGÎ´

end T1Space

end IsGÎ´

section ContinuousAt

variable [TopologicalSpace X]

/-- The set of points where a function is continuous is a GÎ´ set. -/
theorem isGÎ´_setOf_continuousAt [UniformSpace Y] [IsCountablyGenerated (ð“¤ Y)] (f : X â†’ Y) :
    IsGÎ´ { x | ContinuousAt f x } := by
  obtain âŸ¨U, _, hUâŸ© := (@uniformity_hasBasis_open_symmetric Y _).exists_antitone_subbasis
  simp only [Uniform.continuousAt_iff_prod, nhds_prod_eq]
  simp only [(nhds_basis_opens _).prod_self.tendsto_iff hU.toHasBasis, forall_prop_of_true,
    setOf_forall, id]
  refine' isGÎ´_iInter fun k => IsOpen.isGÎ´ <| isOpen_iff_mem_nhds.2 fun x => _
  rintro âŸ¨s, âŸ¨hsx, hsoâŸ©, hsUâŸ©
  filter_upwards [IsOpen.mem_nhds hso hsx] with _ hy using âŸ¨s, âŸ¨hy, hsoâŸ©, hsUâŸ©
#align is_GÎ´_set_of_continuous_at isGÎ´_setOf_continuousAt

end ContinuousAt

section residual

variable [TopologicalSpace X]

/-- A set `s` is called *residual* if it includes a countable intersection of dense open sets. -/
def residual (X : Type*) [TopologicalSpace X] : Filter X :=
  Filter.countableGenerate { t | IsOpen t âˆ§ Dense t }
#align residual residual

instance countableInterFilter_residual : CountableInterFilter (residual X) := by
  rw [residual]; infer_instance
#align countable_Inter_filter_residual countableInterFilter_residual

/-- Dense open sets are residual. -/
theorem residual_of_dense_open {s : Set X} (ho : IsOpen s) (hd : Dense s) : s âˆˆ residual X :=
  CountableGenerateSets.basic âŸ¨ho, hdâŸ©
#align residual_of_dense_open residual_of_dense_open

/-- Dense GÎ´ sets are residual. -/
theorem residual_of_dense_GÎ´ {s : Set X} (ho : IsGÎ´ s) (hd : Dense s) : s âˆˆ residual X := by
  rcases ho with âŸ¨T, To, Tct, rflâŸ©
  exact
    (countable_sInter_mem Tct).mpr fun t tT =>
      residual_of_dense_open (To t tT) (hd.mono (sInter_subset_of_mem tT))
#align residual_of_dense_GÎ´ residual_of_dense_GÎ´

/-- A set is residual iff it includes a countable intersection of dense open sets. -/
theorem mem_residual_iff {s : Set X} :
    s âˆˆ residual X â†”
      âˆƒ S : Set (Set X), (âˆ€ t âˆˆ S, IsOpen t) âˆ§ (âˆ€ t âˆˆ S, Dense t) âˆ§ S.Countable âˆ§ â‹‚â‚€ S âŠ† s :=
  mem_countableGenerate_iff.trans <| by simp_rw [subset_def, mem_setOf, forall_and, and_assoc]
#align mem_residual_iff mem_residual_iff

end residual

section meagre
open Function TopologicalSpace Set
variable {X : Type*} [TopologicalSpace X]

/-- A set is called **nowhere dense** iff its closure has empty interior. -/
def IsNowhereDense (s : Set X) := interior (closure s) = âˆ…

/-- The empty set is nowhere dense. -/
@[simp]
lemma isNowhereDense_empty : IsNowhereDense (âˆ… : Set X) := by
  rw [IsNowhereDense, closure_empty, interior_empty]

/-- A closed set is nowhere dense iff its interior is empty. -/
lemma IsClosed.isNowhereDense_iff {s : Set X} (hs : IsClosed s) :
    IsNowhereDense s â†” interior s = âˆ… := by
  rw [IsNowhereDense, IsClosed.closure_eq hs]

/-- If a set `s` is nowhere dense, so is its closure.-/
protected lemma IsNowhereDense.closure {s : Set X} (hs : IsNowhereDense s) :
    IsNowhereDense (closure s) := by
  rwa [IsNowhereDense, closure_closure]

/-- A nowhere dense set `s` is contained in a closed nowhere dense set (namely, its closure). -/
lemma IsNowhereDense.subset_of_closed_isNowhereDense {s : Set X} (hs : IsNowhereDense s) :
    âˆƒ t : Set X, s âŠ† t âˆ§ IsNowhereDense t âˆ§ IsClosed t :=
  âŸ¨closure s, subset_closure, âŸ¨hs.closure, isClosed_closureâŸ©âŸ©

/-- A set `s` is closed and nowhere dense iff its complement `sá¶œ` is open and dense. -/
lemma isClosed_isNowhereDense_iff_compl {s : Set X} :
    IsClosed s âˆ§ IsNowhereDense s â†” IsOpen sá¶œ âˆ§ Dense sá¶œ := by
  rw [and_congr_right IsClosed.isNowhereDense_iff,
    isOpen_compl_iff, interior_eq_empty_iff_dense_compl]

/-- A set is called **meagre** iff its complement is a residual (or comeagre) set. -/
def IsMeagre (s : Set X) := sá¶œ âˆˆ residual X

/-- The empty set is meagre. -/
lemma meagre_empty : IsMeagre (âˆ… : Set X) := by
  rw [IsMeagre, compl_empty]
  exact Filter.univ_mem

/-- Subsets of meagre sets are meagre. -/
lemma IsMeagre.mono {s t : Set X} (hs : IsMeagre s) (hts: t âŠ† s) : IsMeagre t :=
  Filter.mem_of_superset hs (compl_subset_compl.mpr hts)

/-- An intersection with a meagre set is meagre. -/
lemma IsMeagre.inter {s t : Set X} (hs : IsMeagre s) : IsMeagre (s âˆ© t) :=
  hs.mono (inter_subset_left s t)

/-- A countable union of meagre sets is meagre. -/
lemma isMeagre_iUnion {s : â„• â†’ Set X} (hs : âˆ€ n, IsMeagre (s n)) : IsMeagre (â‹ƒ n, s n) := by
  rw [IsMeagre, compl_iUnion]
  exact countable_iInter_mem.mpr hs

/-- A set is meagre iff it is contained in a countable union of nowhere dense sets. -/
lemma isMeagre_iff_countable_union_isNowhereDense {s : Set X} :
    IsMeagre s â†” âˆƒ S : Set (Set X), (âˆ€ t âˆˆ S, IsNowhereDense t) âˆ§ S.Countable âˆ§ s âŠ† â‹ƒâ‚€ S := by
  rw [IsMeagre, mem_residual_iff, compl_bijective.surjective.image_surjective.exists]
  simp_rw [â† and_assoc, â† forall_and, ball_image_iff, â† isClosed_isNowhereDense_iff_compl,
    sInter_image, â† compl_iUnionâ‚‚, compl_subset_compl, â† sUnion_eq_biUnion, and_assoc]
  refine âŸ¨fun âŸ¨S, hS, hc, hsubâŸ© â†¦ âŸ¨S, fun s hs â†¦ (hS s hs).2, ?_, hsubâŸ©, ?_âŸ©
  Â· rw [â† compl_compl_image S]; exact hc.image _
  Â· intro âŸ¨S, hS, hc, hsubâŸ©
    use closure '' S
    rw [ball_image_iff]
    exact âŸ¨fun s hs â†¦ âŸ¨isClosed_closure, (hS s hs).closureâŸ©,
      (hc.image _).image _, hsub.trans (sUnion_mono_subsets fun s â†¦ subset_closure)âŸ©

end meagre
