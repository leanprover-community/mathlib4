/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Algebra.BigOperators.Finprod
import Mathlib.Topology.ContinuousMap.Algebra
import Mathlib.Topology.Compactness.Paracompact
import Mathlib.Topology.ShrinkingLemma
import Mathlib.Topology.UrysohnsLemma
import Mathlib.Topology.ContinuousMap.Ordered

/-!
# Continuous partition of unity

In this file we define `PartitionOfUnity (Œπ X : Type*) [TopologicalSpace X] (s : Set X := univ)`
to be a continuous partition of unity on `s` indexed by `Œπ`. More precisely,
`f : PartitionOfUnity Œπ X s` is a collection of continuous functions `f i : C(X, ‚Ñù)`, `i : Œπ`,
such that

* the supports of `f i` form a locally finite family of sets;
* each `f i` is nonnegative;
* `‚àë·∂† i, f i x = 1` for all `x ‚àà s`;
* `‚àë·∂† i, f i x ‚â§ 1` for all `x : X`.

In the case `s = univ` the last assumption follows from the previous one but it is convenient to
have this assumption in the case `s ‚â† univ`.

We also define a bump function covering,
`BumpCovering (Œπ X : Type*) [TopologicalSpace X] (s : Set X := univ)`, to be a collection of
functions `f i : C(X, ‚Ñù)`, `i : Œπ`, such that

* the supports of `f i` form a locally finite family of sets;
* each `f i` is nonnegative;
* for each `x ‚àà s` there exists `i : Œπ` such that `f i y = 1` in a neighborhood of `x`.

The term is motivated by the smooth case.

If `f` is a bump function covering indexed by a linearly ordered type, then
`g i x = f i x * ‚àè·∂† j < i, (1 - f j x)` is a partition of unity, see
`BumpCovering.toPartitionOfUnity`. Note that only finitely many terms `1 - f j x` are not equal
to one, so this product is well-defined.

Note that `g i x = ‚àè·∂† j ‚â§ i, (1 - f j x) - ‚àè·∂† j < i, (1 - f j x)`, so most terms in the sum
`‚àë·∂† i, g i x` cancel, and we get `‚àë·∂† i, g i x = 1 - ‚àè·∂† i, (1 - f i x)`, and the latter product
equals zero because one of `f i x` is equal to one.

We say that a partition of unity or a bump function covering `f` is *subordinate* to a family of
sets `U i`, `i : Œπ`, if the closure of the support of each `f i` is included in `U i`. We use
Urysohn's Lemma to prove that a locally finite open covering of a normal topological space admits a
subordinate bump function covering (hence, a subordinate partition of unity), see
`BumpCovering.exists_isSubordinate_of_locallyFinite`. If `X` is a paracompact space, then any
open covering admits a locally finite refinement, hence it admits a subordinate bump function
covering and a subordinate partition of unity, see `BumpCovering.exists_isSubordinate`.

We also provide two slightly more general versions of these lemmas,
`BumpCovering.exists_isSubordinate_of_locallyFinite_of_prop` and
`BumpCovering.exists_isSubordinate_of_prop`, to be used later in the construction of a smooth
partition of unity.

## Implementation notes

Most (if not all) books only define a partition of unity of the whole space. However, quite a few
proofs only deal with `f i` such that `tsupport (f i)` meets a specific closed subset, and
it is easier to formalize these proofs if we don't have other functions right away.

We use `WellOrderingRel j i` instead of `j < i` in the definition of
`BumpCovering.toPartitionOfUnity` to avoid a `[LinearOrder Œπ]` assumption. While
`WellOrderingRel j i` is a well order, not only a strict linear order, we never use this property.

## Tags

partition of unity, bump function, Urysohn's lemma, normal space, paracompact space
-/

universe u v

open Function Set Filter Topology

noncomputable section

/-- A continuous partition of unity on a set `s : Set X` is a collection of continuous functions
`f i` such that

* the supports of `f i` form a locally finite family of sets, i.e., for every point `x : X` there
  exists a neighborhood `U ‚àã x` such that all but finitely many functions `f i` are zero on `U`;
* the functions `f i` are nonnegative;
* the sum `‚àë·∂† i, f i x` is equal to one for every `x ‚àà s` and is less than or equal to one
  otherwise.

If `X` is a normal paracompact space, then `PartitionOfUnity.exists_isSubordinate` guarantees
that for every open covering `U : Set (Set X)` of `s` there exists a partition of unity that is
subordinate to `U`.
-/
structure PartitionOfUnity (Œπ X : Type*) [TopologicalSpace X] (s : Set X := univ) where
  /-- The collection of continuous functions underlying this partition of unity -/
  toFun : Œπ ‚Üí C(X, ‚Ñù)
  /-- the supports of the underlying functions are a locally finite family of sets -/
  locallyFinite' : LocallyFinite fun i => support (toFun i)
  /-- the functions are non-negative -/
  nonneg' : 0 ‚â§ toFun
  /-- the functions sum up to one on `s` -/
  sum_eq_one' : ‚àÄ x ‚àà s, ‚àë·∂† i, toFun i x = 1
  /-- the functions sum up to at most one, globally -/
  sum_le_one' : ‚àÄ x, ‚àë·∂† i, toFun i x ‚â§ 1

/-- A `BumpCovering Œπ X s` is an indexed family of functions `f i`, `i : Œπ`, such that

* the supports of `f i` form a locally finite family of sets, i.e., for every point `x : X` there
  exists a neighborhood `U ‚àã x` such that all but finitely many functions `f i` are zero on `U`;
* for all `i`, `x` we have `0 ‚â§ f i x ‚â§ 1`;
* each point `x ‚àà s` belongs to the interior of `{x | f i x = 1}` for some `i`.

One of the main use cases for a `BumpCovering` is to define a `PartitionOfUnity`, see
`BumpCovering.toPartitionOfUnity`, but some proofs can directly use a `BumpCovering` instead of
a `PartitionOfUnity`.

If `X` is a normal paracompact space, then `BumpCovering.exists_isSubordinate` guarantees that for
every open covering `U : Set (Set X)` of `s` there exists a `BumpCovering` of `s` that is
subordinate to `U`.
-/
structure BumpCovering (Œπ X : Type*) [TopologicalSpace X] (s : Set X := univ) where
  /-- The collections of continuous functions underlying this bump covering -/
  toFun : Œπ ‚Üí C(X, ‚Ñù)
  /-- the supports of the underlying functions are a locally finite family of sets -/
  locallyFinite' : LocallyFinite fun i => support (toFun i)
  /-- the functions are non-negative -/
  nonneg' : 0 ‚â§ toFun
  /-- the functions are each at most one -/
  le_one' : toFun ‚â§ 1
  /-- Each point `x ‚àà s` belongs to the interior of `{x | f i x = 1}` for some `i`. -/
  eventuallyEq_one' : ‚àÄ x ‚àà s, ‚àÉ i, toFun i =·∂†[ùìù x] 1

variable {Œπ : Type u} {X : Type v} [TopologicalSpace X]

namespace PartitionOfUnity

variable {E : Type*} [AddCommMonoid E] [SMulWithZero ‚Ñù E] [TopologicalSpace E] [ContinuousSMul ‚Ñù E]
  {s : Set X} (f : PartitionOfUnity Œπ X s)

instance : FunLike (PartitionOfUnity Œπ X s) Œπ C(X, ‚Ñù) where
  coe := toFun
  coe_injective' f g h := by cases f; cases g; congr

protected theorem locallyFinite : LocallyFinite fun i => support (f i) :=
  f.locallyFinite'

theorem locallyFinite_tsupport : LocallyFinite fun i => tsupport (f i) :=
  f.locallyFinite.closure

theorem nonneg (i : Œπ) (x : X) : 0 ‚â§ f i x :=
  f.nonneg' i x

theorem sum_eq_one {x : X} (hx : x ‚àà s) : ‚àë·∂† i, f i x = 1 :=
  f.sum_eq_one' x hx

/-- If `f` is a partition of unity on `s`, then for every `x ‚àà s` there exists an index `i` such
that `0 < f i x`. -/
theorem exists_pos {x : X} (hx : x ‚àà s) : ‚àÉ i, 0 < f i x := by
  have H := f.sum_eq_one hx
  contrapose! H
  simpa only [fun i => (H i).antisymm (f.nonneg i x), finsum_zero] using zero_ne_one

theorem sum_le_one (x : X) : ‚àë·∂† i, f i x ‚â§ 1 :=
  f.sum_le_one' x

theorem sum_nonneg (x : X) : 0 ‚â§ ‚àë·∂† i, f i x :=
  finsum_nonneg fun i => f.nonneg i x

theorem le_one (i : Œπ) (x : X) : f i x ‚â§ 1 :=
  (single_le_finsum i (f.locallyFinite.point_finite x) fun j => f.nonneg j x).trans (f.sum_le_one x)

section finsupport

variable {s : Set X} (œÅ : PartitionOfUnity Œπ X s) (x‚ÇÄ : X)

/-- The support of a partition of unity at a point `x‚ÇÄ` as a `Finset`.
  This is the set of `i : Œπ` such that `x‚ÇÄ ‚àà support f i`, i.e. `f i ‚â† x‚ÇÄ`. -/
def finsupport : Finset Œπ := (œÅ.locallyFinite.point_finite x‚ÇÄ).toFinset

@[simp]
theorem mem_finsupport (x‚ÇÄ : X) {i} :
    i ‚àà œÅ.finsupport x‚ÇÄ ‚Üî i ‚àà support fun i ‚Ü¶ œÅ i x‚ÇÄ := by
  simp only [finsupport, mem_support, Finite.mem_toFinset, mem_setOf_eq]

@[simp]
theorem coe_finsupport (x‚ÇÄ : X) :
    (œÅ.finsupport x‚ÇÄ : Set Œπ) = support fun i ‚Ü¶ œÅ i x‚ÇÄ := by
  ext
  rw [Finset.mem_coe, mem_finsupport]

variable {x‚ÇÄ : X}

theorem sum_finsupport (hx‚ÇÄ : x‚ÇÄ ‚àà s) : ‚àë i ‚àà œÅ.finsupport x‚ÇÄ, œÅ i x‚ÇÄ = 1 := by
  rw [‚Üê œÅ.sum_eq_one hx‚ÇÄ, finsum_eq_sum_of_support_subset _ (œÅ.coe_finsupport x‚ÇÄ).superset]

theorem sum_finsupport' (hx‚ÇÄ : x‚ÇÄ ‚àà s) {I : Finset Œπ} (hI : œÅ.finsupport x‚ÇÄ ‚äÜ I) :
    ‚àë i ‚àà I, œÅ i x‚ÇÄ = 1 := by
  classical
  rw [‚Üê Finset.sum_sdiff hI, œÅ.sum_finsupport hx‚ÇÄ]
  suffices ‚àë i ‚àà I \ œÅ.finsupport x‚ÇÄ, (œÅ i) x‚ÇÄ = ‚àë i ‚àà I \ œÅ.finsupport x‚ÇÄ, 0 by
    rw [this, add_eq_right, Finset.sum_const_zero]
  apply Finset.sum_congr rfl
  rintro x hx
  simp only [Finset.mem_sdiff, œÅ.mem_finsupport, mem_support, Classical.not_not] at hx
  exact hx.2

theorem sum_finsupport_smul_eq_finsum {M : Type*} [AddCommMonoid M] [Module ‚Ñù M] (œÜ : Œπ ‚Üí X ‚Üí M) :
    ‚àë i ‚àà œÅ.finsupport x‚ÇÄ, œÅ i x‚ÇÄ ‚Ä¢ œÜ i x‚ÇÄ = ‚àë·∂† i, œÅ i x‚ÇÄ ‚Ä¢ œÜ i x‚ÇÄ := by
  apply (finsum_eq_sum_of_support_subset _ _).symm
  have : (fun i ‚Ü¶ (œÅ i) x‚ÇÄ ‚Ä¢ œÜ i x‚ÇÄ) = (fun i ‚Ü¶ (œÅ i) x‚ÇÄ) ‚Ä¢ (fun i ‚Ü¶ œÜ i x‚ÇÄ) :=
    funext fun _ => (Pi.smul_apply' _ _ _).symm
  rw [œÅ.coe_finsupport x‚ÇÄ, this, support_smul]
  exact inter_subset_left

end finsupport

section fintsupport -- partitions of unity have locally finite `tsupport`

variable {s : Set X} (œÅ : PartitionOfUnity Œπ X s) (x‚ÇÄ : X)

/-- The `tsupport`s of a partition of unity are locally finite. -/
theorem finite_tsupport : {i | x‚ÇÄ ‚àà tsupport (œÅ i)}.Finite := by
  rcases œÅ.locallyFinite x‚ÇÄ with ‚ü®t, t_in, ht‚ü©
  apply ht.subset
  rintro i hi
  simp only [inter_comm]
  exact mem_closure_iff_nhds.mp hi t t_in

/-- The tsupport of a partition of unity at a point `x‚ÇÄ` as a `Finset`.
  This is the set of `i : Œπ` such that `x‚ÇÄ ‚àà tsupport f i`. -/
def fintsupport (x‚ÇÄ : X) : Finset Œπ :=
  (œÅ.finite_tsupport x‚ÇÄ).toFinset

theorem mem_fintsupport_iff (i : Œπ) : i ‚àà œÅ.fintsupport x‚ÇÄ ‚Üî x‚ÇÄ ‚àà tsupport (œÅ i) :=
  Finite.mem_toFinset _

theorem eventually_fintsupport_subset :
    ‚àÄ·∂† y in ùìù x‚ÇÄ, œÅ.fintsupport y ‚äÜ œÅ.fintsupport x‚ÇÄ := by
  apply (œÅ.locallyFinite.closure.eventually_subset (fun _ ‚Ü¶ isClosed_closure) x‚ÇÄ).mono
  intro y hy z hz
  rw [PartitionOfUnity.mem_fintsupport_iff] at *
  exact hy hz

theorem finsupport_subset_fintsupport : œÅ.finsupport x‚ÇÄ ‚äÜ œÅ.fintsupport x‚ÇÄ := fun i hi ‚Ü¶ by
  rw [œÅ.mem_fintsupport_iff]
  apply subset_closure
  exact (œÅ.mem_finsupport x‚ÇÄ).mp hi

theorem eventually_finsupport_subset : ‚àÄ·∂† y in ùìù x‚ÇÄ, œÅ.finsupport y ‚äÜ œÅ.fintsupport x‚ÇÄ :=
  (œÅ.eventually_fintsupport_subset x‚ÇÄ).mono
    fun y hy ‚Ü¶ (œÅ.finsupport_subset_fintsupport y).trans hy

end fintsupport

/-- If `f` is a partition of unity on `s : Set X` and `g : X ‚Üí E` is continuous at every point of
the topological support of some `f i`, then `fun x ‚Ü¶ f i x ‚Ä¢ g x` is continuous on the whole space.
-/
theorem continuous_smul {g : X ‚Üí E} {i : Œπ} (hg : ‚àÄ x ‚àà tsupport (f i), ContinuousAt g x) :
    Continuous fun x => f i x ‚Ä¢ g x :=
  continuous_of_tsupport fun x hx =>
    ((f i).continuousAt x).smul <| hg x <| tsupport_smul_subset_left _ _ hx

/-- If `f` is a partition of unity on a set `s : Set X` and `g : Œπ ‚Üí X ‚Üí E` is a family of functions
such that each `g i` is continuous at every point of the topological support of `f i`, then the sum
`fun x ‚Ü¶ ‚àë·∂† i, f i x ‚Ä¢ g i x` is continuous on the whole space. -/
theorem continuous_finsum_smul [ContinuousAdd E] {g : Œπ ‚Üí X ‚Üí E}
    (hg : ‚àÄ (i), ‚àÄ x ‚àà tsupport (f i), ContinuousAt (g i) x) :
    Continuous fun x => ‚àë·∂† i, f i x ‚Ä¢ g i x :=
  (continuous_finsum fun i => f.continuous_smul (hg i)) <|
    f.locallyFinite.subset fun _ => support_smul_subset_left _ _

/-- A partition of unity `f i` is subordinate to a family of sets `U i` indexed by the same type if
for each `i` the closure of the support of `f i` is a subset of `U i`. -/
def IsSubordinate (U : Œπ ‚Üí Set X) : Prop :=
  ‚àÄ i, tsupport (f i) ‚äÜ U i

variable {f}

theorem exists_finset_nhds' {s : Set X} (œÅ : PartitionOfUnity Œπ X s) (x‚ÇÄ : X) :
    ‚àÉ I : Finset Œπ, (‚àÄ·∂† x in ùìù[s] x‚ÇÄ, ‚àë i ‚àà I, œÅ i x = 1) ‚àß
      ‚àÄ·∂† x in ùìù x‚ÇÄ, support (œÅ ¬∑ x) ‚äÜ I := by
  rcases œÅ.locallyFinite.exists_finset_support x‚ÇÄ with ‚ü®I, hI‚ü©
  refine ‚ü®I, eventually_nhdsWithin_iff.mpr (hI.mono fun x hx x_in ‚Ü¶ ?_), hI‚ü©
  have : ‚àë·∂† i : Œπ, œÅ i x = ‚àë i ‚àà I, œÅ i x := finsum_eq_sum_of_support_subset _ hx
  rwa [eq_comm, œÅ.sum_eq_one x_in] at this

@[deprecated (since := "2025-05-22")] alias exists_finset_nhd' := exists_finset_nhds'

theorem exists_finset_nhds (œÅ : PartitionOfUnity Œπ X univ) (x‚ÇÄ : X) :
    ‚àÉ I : Finset Œπ, ‚àÄ·∂† x in ùìù x‚ÇÄ, ‚àë i ‚àà I, œÅ i x = 1 ‚àß support (œÅ ¬∑ x) ‚äÜ I := by
  rcases œÅ.exists_finset_nhds' x‚ÇÄ with ‚ü®I, H‚ü©
  use I
  rwa [nhdsWithin_univ, ‚Üê eventually_and] at H

@[deprecated (since := "2025-05-22")] alias exists_finset_nhd := exists_finset_nhds

theorem exists_finset_nhds_support_subset {U : Œπ ‚Üí Set X} (hso : f.IsSubordinate U)
    (ho : ‚àÄ i, IsOpen (U i)) (x : X) :
    ‚àÉ is : Finset Œπ, ‚àÉ n ‚àà ùìù x, n ‚äÜ ‚ãÇ i ‚àà is, U i ‚àß ‚àÄ z ‚àà n, (support (f ¬∑ z)) ‚äÜ is :=
  f.locallyFinite.exists_finset_nhds_support_subset hso ho x

@[deprecated (since := "2025-05-22")]
alias exists_finset_nhd_support_subset := exists_finset_nhds_support_subset

/-- If `f` is a partition of unity that is subordinate to a family of open sets `U i` and
`g : Œπ ‚Üí X ‚Üí E` is a family of functions such that each `g i` is continuous on `U i`, then the sum
`fun x ‚Ü¶ ‚àë·∂† i, f i x ‚Ä¢ g i x` is a continuous function. -/
theorem IsSubordinate.continuous_finsum_smul [ContinuousAdd E] {U : Œπ ‚Üí Set X}
    (ho : ‚àÄ i, IsOpen (U i)) (hf : f.IsSubordinate U) {g : Œπ ‚Üí X ‚Üí E}
    (hg : ‚àÄ i, ContinuousOn (g i) (U i)) : Continuous fun x => ‚àë·∂† i, f i x ‚Ä¢ g i x :=
  f.continuous_finsum_smul fun i _ hx => (hg i).continuousAt <| (ho i).mem_nhds <| hf i hx

end PartitionOfUnity

namespace BumpCovering

variable {s : Set X} (f : BumpCovering Œπ X s)

instance : FunLike (BumpCovering Œπ X s) Œπ C(X, ‚Ñù) where
  coe := toFun
  coe_injective' f g h := by cases f; cases g; congr

@[simp] lemma toFun_eq_coe : f.toFun = f := rfl

protected theorem locallyFinite : LocallyFinite fun i => support (f i) :=
  f.locallyFinite'

theorem locallyFinite_tsupport : LocallyFinite fun i => tsupport (f i) :=
  f.locallyFinite.closure

protected theorem point_finite (x : X) : { i | f i x ‚â† 0 }.Finite :=
  f.locallyFinite.point_finite x

theorem nonneg (i : Œπ) (x : X) : 0 ‚â§ f i x :=
  f.nonneg' i x

theorem le_one (i : Œπ) (x : X) : f i x ‚â§ 1 :=
  f.le_one' i x

open Classical in
/-- A `BumpCovering` that consists of a single function, uniformly equal to one, defined as an
example for `Inhabited` instance. -/
protected def single (i : Œπ) (s : Set X) : BumpCovering Œπ X s where
  toFun := Pi.single i 1
  locallyFinite' x := by
    refine ‚ü®univ, univ_mem, (finite_singleton i).subset ?_‚ü©
    rintro j ‚ü®x, hx, -‚ü©
    contrapose! hx
    rw [mem_singleton_iff] at hx
    simp [hx]
  nonneg' := le_update_iff.2 ‚ü®fun _ => zero_le_one, fun _ _ => le_rfl‚ü©
  le_one' := update_le_iff.2 ‚ü®le_rfl, fun _ _ _ => zero_le_one‚ü©
  eventuallyEq_one' x _ := ‚ü®i, by rw [Pi.single_eq_same, ContinuousMap.coe_one]‚ü©

open Classical in
@[simp]
theorem coe_single (i : Œπ) (s : Set X) : ‚áë(BumpCovering.single i s) = Pi.single i 1 := by
  rfl

instance [Inhabited Œπ] : Inhabited (BumpCovering Œπ X s) :=
  ‚ü®BumpCovering.single default s‚ü©

/-- A collection of bump functions `f i` is subordinate to a family of sets `U i` indexed by the
same type if for each `i` the closure of the support of `f i` is a subset of `U i`. -/
def IsSubordinate (f : BumpCovering Œπ X s) (U : Œπ ‚Üí Set X) : Prop :=
  ‚àÄ i, tsupport (f i) ‚äÜ U i

theorem IsSubordinate.mono {f : BumpCovering Œπ X s} {U V : Œπ ‚Üí Set X} (hU : f.IsSubordinate U)
    (hV : ‚àÄ i, U i ‚äÜ V i) : f.IsSubordinate V :=
  fun i => Subset.trans (hU i) (hV i)

/-- If `X` is a normal topological space and `U i`, `i : Œπ`, is a locally finite open covering of a
closed set `s`, then there exists a `BumpCovering Œπ X s` that is subordinate to `U`. If `X` is a
paracompact space, then the assumption `hf : LocallyFinite U` can be omitted, see
`BumpCovering.exists_isSubordinate`. This version assumes that `p : (X ‚Üí ‚Ñù) ‚Üí Prop` is a predicate
that satisfies Urysohn's lemma, and provides a `BumpCovering` such that each function of the
covering satisfies `p`. -/
theorem exists_isSubordinate_of_locallyFinite_of_prop [NormalSpace X] (p : (X ‚Üí ‚Ñù) ‚Üí Prop)
    (h01 : ‚àÄ s t, IsClosed s ‚Üí IsClosed t ‚Üí Disjoint s t ‚Üí
      ‚àÉ f : C(X, ‚Ñù), p f ‚àß EqOn f 0 s ‚àß EqOn f 1 t ‚àß ‚àÄ x, f x ‚àà Icc (0 : ‚Ñù) 1)
    (hs : IsClosed s) (U : Œπ ‚Üí Set X) (ho : ‚àÄ i, IsOpen (U i)) (hf : LocallyFinite U)
    (hU : s ‚äÜ ‚ãÉ i, U i) : ‚àÉ f : BumpCovering Œπ X s, (‚àÄ i, p (f i)) ‚àß f.IsSubordinate U := by
  rcases exists_subset_iUnion_closure_subset hs ho (fun x _ => hf.point_finite x) hU with
    ‚ü®V, hsV, hVo, hVU‚ü©
  have hVU' : ‚àÄ i, V i ‚äÜ U i := fun i => Subset.trans subset_closure (hVU i)
  rcases exists_subset_iUnion_closure_subset hs hVo (fun x _ => (hf.subset hVU').point_finite x)
      hsV with
    ‚ü®W, hsW, hWo, hWV‚ü©
  choose f hfp hf0 hf1 hf01 using fun i =>
    h01 _ _ (isClosed_compl_iff.2 <| hVo i) isClosed_closure
      (disjoint_right.2 fun x hx => Classical.not_not.2 (hWV i hx))
  have hsupp : ‚àÄ i, support (f i) ‚äÜ V i := fun i => support_subset_iff'.2 (hf0 i)
  refine ‚ü®‚ü®f, hf.subset fun i => Subset.trans (hsupp i) (hVU' i), fun i x => (hf01 i x).1,
      fun i x => (hf01 i x).2, fun x hx => ?_‚ü©,
    hfp, fun i => Subset.trans (closure_mono (hsupp i)) (hVU i)‚ü©
  rcases mem_iUnion.1 (hsW hx) with ‚ü®i, hi‚ü©
  exact ‚ü®i, ((hf1 i).mono subset_closure).eventuallyEq_of_mem ((hWo i).mem_nhds hi)‚ü©

/-- If `X` is a normal topological space and `U i`, `i : Œπ`, is a locally finite open covering of a
closed set `s`, then there exists a `BumpCovering Œπ X s` that is subordinate to `U`. If `X` is a
paracompact space, then the assumption `hf : LocallyFinite U` can be omitted, see
`BumpCovering.exists_isSubordinate`. -/
theorem exists_isSubordinate_of_locallyFinite [NormalSpace X] (hs : IsClosed s) (U : Œπ ‚Üí Set X)
    (ho : ‚àÄ i, IsOpen (U i)) (hf : LocallyFinite U) (hU : s ‚äÜ ‚ãÉ i, U i) :
    ‚àÉ f : BumpCovering Œπ X s, f.IsSubordinate U :=
  let ‚ü®f, _, hfU‚ü© :=
    exists_isSubordinate_of_locallyFinite_of_prop (fun _ => True)
      (fun _ _ hs ht hd =>
        (exists_continuous_zero_one_of_isClosed hs ht hd).imp fun _ hf => ‚ü®trivial, hf‚ü©)
      hs U ho hf hU
  ‚ü®f, hfU‚ü©

/-- If `X` is a paracompact normal topological space and `U` is an open covering of a closed set
`s`, then there exists a `BumpCovering Œπ X s` that is subordinate to `U`. This version assumes that
`p : (X ‚Üí ‚Ñù) ‚Üí Prop` is a predicate that satisfies Urysohn's lemma, and provides a
`BumpCovering` such that each function of the covering satisfies `p`. -/
theorem exists_isSubordinate_of_prop [NormalSpace X] [ParacompactSpace X] (p : (X ‚Üí ‚Ñù) ‚Üí Prop)
    (h01 : ‚àÄ s t, IsClosed s ‚Üí IsClosed t ‚Üí Disjoint s t ‚Üí
      ‚àÉ f : C(X, ‚Ñù), p f ‚àß EqOn f 0 s ‚àß EqOn f 1 t ‚àß ‚àÄ x, f x ‚àà Icc (0 : ‚Ñù) 1)
    (hs : IsClosed s) (U : Œπ ‚Üí Set X) (ho : ‚àÄ i, IsOpen (U i)) (hU : s ‚äÜ ‚ãÉ i, U i) :
    ‚àÉ f : BumpCovering Œπ X s, (‚àÄ i, p (f i)) ‚àß f.IsSubordinate U := by
  rcases precise_refinement_set hs _ ho hU with ‚ü®V, hVo, hsV, hVf, hVU‚ü©
  rcases exists_isSubordinate_of_locallyFinite_of_prop p h01 hs V hVo hVf hsV with ‚ü®f, hfp, hf‚ü©
  exact ‚ü®f, hfp, hf.mono hVU‚ü©

/-- If `X` is a paracompact normal topological space and `U` is an open covering of a closed set
`s`, then there exists a `BumpCovering Œπ X s` that is subordinate to `U`. -/
theorem exists_isSubordinate [NormalSpace X] [ParacompactSpace X] (hs : IsClosed s) (U : Œπ ‚Üí Set X)
    (ho : ‚àÄ i, IsOpen (U i)) (hU : s ‚äÜ ‚ãÉ i, U i) : ‚àÉ f : BumpCovering Œπ X s, f.IsSubordinate U := by
  rcases precise_refinement_set hs _ ho hU with ‚ü®V, hVo, hsV, hVf, hVU‚ü©
  rcases exists_isSubordinate_of_locallyFinite hs V hVo hVf hsV with ‚ü®f, hf‚ü©
  exact ‚ü®f, hf.mono hVU‚ü©

/-- If `X` is a locally compact T2 topological space and `U i`, `i : Œπ`, is a locally finite open
covering of a compact set `s`, then there exists a `BumpCovering Œπ X s` that is subordinate to `U`.
If `X` is a paracompact space, then the assumption `hf : LocallyFinite U` can be omitted, see
`BumpCovering.exists_isSubordinate`. This version assumes that `p : (X ‚Üí ‚Ñù) ‚Üí Prop` is a predicate
that satisfies Urysohn's lemma, and provides a `BumpCovering` such that each function of the
covering satisfies `p`. -/
theorem exists_isSubordinate_of_locallyFinite_of_prop_t2space [LocallyCompactSpace X] [T2Space X]
    (p : (X ‚Üí ‚Ñù) ‚Üí Prop) (h01 : ‚àÄ s t, IsClosed s ‚Üí IsCompact t ‚Üí Disjoint s t ‚Üí ‚àÉ f : C(X, ‚Ñù),
    p f ‚àß EqOn f 0 s ‚àß EqOn f 1 t ‚àß ‚àÄ x, f x ‚àà Icc (0 : ‚Ñù) 1)
    (hs : IsCompact s) (U : Œπ ‚Üí Set X) (ho : ‚àÄ i, IsOpen (U i)) (hf : LocallyFinite U)
    (hU : s ‚äÜ ‚ãÉ i, U i) :
    ‚àÉ f : BumpCovering Œπ X s, (‚àÄ i, p (f i)) ‚àß f.IsSubordinate U ‚àß
      ‚àÄ i, HasCompactSupport (f i) := by
  rcases exists_subset_iUnion_closure_subset_t2space hs ho (fun x _ => hf.point_finite x) hU with
    ‚ü®V, hsV, hVo, hVU, hcp‚ü©
  have hVU' i : V i ‚äÜ U i := subset_closure.trans (hVU i)
  rcases exists_subset_iUnion_closure_subset_t2space hs hVo
    (fun x _ => (hf.subset hVU').point_finite x) hsV with ‚ü®W, hsW, hWo, hWV, hWc‚ü©
  choose f hfp hf0 hf1 hf01 using fun i =>
    h01 _ _ (isClosed_compl_iff.2 <| hVo i) (hWc i)
      (disjoint_right.2 fun x hx => Classical.not_not.2 (hWV i hx))
  have hsupp i : support (f i) ‚äÜ V i := support_subset_iff'.2 (hf0 i)
  refine ‚ü®‚ü®f, hf.subset fun i => Subset.trans (hsupp i) (hVU' i), fun i x => (hf01 i x).1,
      fun i x => (hf01 i x).2, fun x hx => ?_‚ü©,
    hfp, fun i => Subset.trans (closure_mono (hsupp i)) (hVU i),
    fun i => IsCompact.of_isClosed_subset (hcp i) isClosed_closure <| closure_mono (hsupp i)‚ü©
  rcases mem_iUnion.1 (hsW hx) with ‚ü®i, hi‚ü©
  exact ‚ü®i, ((hf1 i).mono subset_closure).eventuallyEq_of_mem ((hWo i).mem_nhds hi)‚ü©

/-- If `X` is a normal topological space and `U i`, `i : Œπ`, is a locally finite open covering of a
closed set `s`, then there exists a `BumpCovering Œπ X s` that is subordinate to `U`. If `X` is a
paracompact space, then the assumption `hf : LocallyFinite U` can be omitted, see
`BumpCovering.exists_isSubordinate`. -/
theorem exists_isSubordinate_hasCompactSupport_of_locallyFinite_t2space [LocallyCompactSpace X]
    [T2Space X]
    (hs : IsCompact s) (U : Œπ ‚Üí Set X) (ho : ‚àÄ i, IsOpen (U i)) (hf : LocallyFinite U)
    (hU : s ‚äÜ ‚ãÉ i, U i) :
    ‚àÉ f : BumpCovering Œπ X s, f.IsSubordinate U ‚àß ‚àÄ i, HasCompactSupport (f i) := by
  -- need to switch 0 and 1 in `exists_continuous_zero_one_of_isCompact`
  simpa using
    exists_isSubordinate_of_locallyFinite_of_prop_t2space (fun _ => True)
      (fun _ _ ht hs hd =>
        (exists_continuous_zero_one_of_isCompact' hs ht hd.symm).imp fun _ hf => ‚ü®trivial, hf‚ü©)
      hs U ho hf hU

/-- Index of a bump function such that `fs i =·∂†[ùìù x] 1`. -/
def ind (x : X) (hx : x ‚àà s) : Œπ :=
  (f.eventuallyEq_one' x hx).choose

theorem eventuallyEq_one (x : X) (hx : x ‚àà s) : f (f.ind x hx) =·∂†[ùìù x] 1 :=
  (f.eventuallyEq_one' x hx).choose_spec

theorem ind_apply (x : X) (hx : x ‚àà s) : f (f.ind x hx) x = 1 :=
  (f.eventuallyEq_one x hx).eq_of_nhds

/-- Partition of unity defined by a `BumpCovering`. We use this auxiliary definition to prove some
properties of the new family of functions before bundling it into a `PartitionOfUnity`. Do not use
this definition, use `BumpCovering.toPartitionOfUnity` instead.

The partition of unity is given by the formula `g i x = f i x * ‚àè·∂† j < i, (1 - f j x)`. In other
words, `g i x = ‚àè·∂† j < i, (1 - f j x) - ‚àè·∂† j ‚â§ i, (1 - f j x)`, so
`‚àë·∂† i, g i x = 1 - ‚àè·∂† j, (1 - f j x)`. If `x ‚àà s`, then one of `f j x` equals one, hence the product
of `1 - f j x` vanishes, and `‚àë·∂† i, g i x = 1`.

In order to avoid an assumption `LinearOrder Œπ`, we use `WellOrderingRel` instead of `(<)`. -/
def toPOUFun (i : Œπ) (x : X) : ‚Ñù :=
  f i x * ‚àè·∂† (j) (_ : WellOrderingRel j i), (1 - f j x)

theorem toPOUFun_zero_of_zero {i : Œπ} {x : X} (h : f i x = 0) : f.toPOUFun i x = 0 := by
  rw [toPOUFun, h, zero_mul]

theorem support_toPOUFun_subset (i : Œπ) : support (f.toPOUFun i) ‚äÜ support (f i) :=
  fun _ => mt <| f.toPOUFun_zero_of_zero

open Classical in
theorem toPOUFun_eq_mul_prod (i : Œπ) (x : X) (t : Finset Œπ)
    (ht : ‚àÄ j, WellOrderingRel j i ‚Üí f j x ‚â† 0 ‚Üí j ‚àà t) :
    f.toPOUFun i x = f i x * ‚àè j ‚àà t with WellOrderingRel j i, (1 - f j x) := by
  refine congr_arg _ (finprod_cond_eq_prod_of_cond_iff _ fun {j} hj => ?_)
  rw [Ne, sub_eq_self] at hj
  rw [Finset.mem_filter, Iff.comm, and_iff_right_iff_imp]
  exact flip (ht j) hj

theorem sum_toPOUFun_eq (x : X) : ‚àë·∂† i, f.toPOUFun i x = 1 - ‚àè·∂† i, (1 - f i x) := by
  set s := (f.point_finite x).toFinset
  have hs : (s : Set Œπ) = { i | f i x ‚â† 0 } := Finite.coe_toFinset _
  have A : (support fun i => toPOUFun f i x) ‚äÜ s := by
    rw [hs]
    exact fun i hi => f.support_toPOUFun_subset i hi
  have B : (mulSupport fun i => 1 - f i x) ‚äÜ s := by
    rw [hs, mulSupport_one_sub]
    exact fun i => id
  classical
  letI : LinearOrder Œπ := linearOrderOfSTO WellOrderingRel
  rw [finsum_eq_sum_of_support_subset _ A, finprod_eq_prod_of_mulSupport_subset _ B,
    Finset.prod_one_sub_ordered, sub_sub_cancel]
  refine Finset.sum_congr rfl fun i _ => ?_
  convert f.toPOUFun_eq_mul_prod _ _ _ fun j _ hj => _
  rwa [Finite.mem_toFinset]

open Classical in
theorem exists_finset_toPOUFun_eventuallyEq (i : Œπ) (x : X) : ‚àÉ t : Finset Œπ,
    f.toPOUFun i =·∂†[ùìù x] f i * ‚àè j ‚àà t with WellOrderingRel j i, (1 - f j) := by
  rcases f.locallyFinite x with ‚ü®U, hU, hf‚ü©
  use hf.toFinset
  filter_upwards [hU] with y hyU
  simp only [ContinuousMap.coe_prod, Pi.mul_apply, Finset.prod_apply]
  apply toPOUFun_eq_mul_prod
  intro j _ hj
  exact hf.mem_toFinset.2 ‚ü®y, ‚ü®hj, hyU‚ü©‚ü©

theorem continuous_toPOUFun (i : Œπ) : Continuous (f.toPOUFun i) := by
  refine (map_continuous <| f i).mul <| continuous_finprod_cond (fun j _ => by fun_prop) ?_
  simp only [mulSupport_one_sub]
  exact f.locallyFinite

/-- The partition of unity defined by a `BumpCovering`.

The partition of unity is given by the formula `g i x = f i x * ‚àè·∂† j < i, (1 - f j x)`. In other
words, `g i x = ‚àè·∂† j < i, (1 - f j x) - ‚àè·∂† j ‚â§ i, (1 - f j x)`, so
`‚àë·∂† i, g i x = 1 - ‚àè·∂† j, (1 - f j x)`. If `x ‚àà s`, then one of `f j x` equals one, hence the product
of `1 - f j x` vanishes, and `‚àë·∂† i, g i x = 1`.

In order to avoid an assumption `LinearOrder Œπ`, we use `WellOrderingRel` instead of `(<)`. -/
def toPartitionOfUnity : PartitionOfUnity Œπ X s where
  toFun i := ‚ü®f.toPOUFun i, f.continuous_toPOUFun i‚ü©
  locallyFinite' := f.locallyFinite.subset f.support_toPOUFun_subset
  nonneg' i x :=
    mul_nonneg (f.nonneg i x) (finprod_cond_nonneg fun j _ => sub_nonneg.2 <| f.le_one j x)
  sum_eq_one' x hx := by
    simp only [ContinuousMap.coe_mk, sum_toPOUFun_eq, sub_eq_self]
    apply finprod_eq_zero (fun i => 1 - f i x) (f.ind x hx)
    ¬∑ simp only [f.ind_apply x hx, sub_self]
    ¬∑ rw [mulSupport_one_sub]
      exact f.point_finite x
  sum_le_one' x := by
    simp only [ContinuousMap.coe_mk, sum_toPOUFun_eq, sub_le_self_iff]
    exact finprod_nonneg fun i => sub_nonneg.2 <| f.le_one i x

theorem toPartitionOfUnity_apply (i : Œπ) (x : X) :
    f.toPartitionOfUnity i x = f i x * ‚àè·∂† (j) (_ : WellOrderingRel j i), (1 - f j x) := rfl

open Classical in
theorem toPartitionOfUnity_eq_mul_prod (i : Œπ) (x : X) (t : Finset Œπ)
    (ht : ‚àÄ j, WellOrderingRel j i ‚Üí f j x ‚â† 0 ‚Üí j ‚àà t) :
    f.toPartitionOfUnity i x = f i x * ‚àè j ‚àà t with WellOrderingRel j i, (1 - f j x) :=
  f.toPOUFun_eq_mul_prod i x t ht

open Classical in
theorem exists_finset_toPartitionOfUnity_eventuallyEq (i : Œπ) (x : X) : ‚àÉ t : Finset Œπ,
    f.toPartitionOfUnity i =·∂†[ùìù x] f i * ‚àè j ‚àà t with WellOrderingRel j i, (1 - f j) :=
  f.exists_finset_toPOUFun_eventuallyEq i x

theorem toPartitionOfUnity_zero_of_zero {i : Œπ} {x : X} (h : f i x = 0) :
    f.toPartitionOfUnity i x = 0 :=
  f.toPOUFun_zero_of_zero h

theorem support_toPartitionOfUnity_subset (i : Œπ) :
    support (f.toPartitionOfUnity i) ‚äÜ support (f i) :=
  f.support_toPOUFun_subset i

theorem sum_toPartitionOfUnity_eq (x : X) :
    ‚àë·∂† i, f.toPartitionOfUnity i x = 1 - ‚àè·∂† i, (1 - f i x) :=
  f.sum_toPOUFun_eq x

theorem IsSubordinate.toPartitionOfUnity {f : BumpCovering Œπ X s} {U : Œπ ‚Üí Set X}
    (h : f.IsSubordinate U) : f.toPartitionOfUnity.IsSubordinate U :=
  fun i => Subset.trans (closure_mono <| f.support_toPartitionOfUnity_subset i) (h i)

end BumpCovering

namespace PartitionOfUnity

variable {s : Set X}

instance [Inhabited Œπ] : Inhabited (PartitionOfUnity Œπ X s) :=
  ‚ü®BumpCovering.toPartitionOfUnity default‚ü©

/-- If `X` is a normal topological space and `U` is a locally finite open covering of a closed set
`s`, then there exists a `PartitionOfUnity Œπ X s` that is subordinate to `U`. If `X` is a
paracompact space, then the assumption `hf : LocallyFinite U` can be omitted, see
`BumpCovering.exists_isSubordinate`. -/
theorem exists_isSubordinate_of_locallyFinite [NormalSpace X] (hs : IsClosed s) (U : Œπ ‚Üí Set X)
    (ho : ‚àÄ i, IsOpen (U i)) (hf : LocallyFinite U) (hU : s ‚äÜ ‚ãÉ i, U i) :
    ‚àÉ f : PartitionOfUnity Œπ X s, f.IsSubordinate U :=
  let ‚ü®f, hf‚ü© := BumpCovering.exists_isSubordinate_of_locallyFinite hs U ho hf hU
  ‚ü®f.toPartitionOfUnity, hf.toPartitionOfUnity‚ü©

/-- If `X` is a paracompact normal topological space and `U` is an open covering of a closed set
`s`, then there exists a `PartitionOfUnity Œπ X s` that is subordinate to `U`. -/
theorem exists_isSubordinate [NormalSpace X] [ParacompactSpace X] (hs : IsClosed s) (U : Œπ ‚Üí Set X)
    (ho : ‚àÄ i, IsOpen (U i)) (hU : s ‚äÜ ‚ãÉ i, U i) :
    ‚àÉ f : PartitionOfUnity Œπ X s, f.IsSubordinate U :=
  let ‚ü®f, hf‚ü© := BumpCovering.exists_isSubordinate hs U ho hU
  ‚ü®f.toPartitionOfUnity, hf.toPartitionOfUnity‚ü©

/-- If `X` is a locally compact T2 topological space and `U` is a locally finite open covering of a
compact set `s`, then there exists a `PartitionOfUnity Œπ X s` that is subordinate to `U`. -/
theorem exists_isSubordinate_of_locallyFinite_t2space [LocallyCompactSpace X] [T2Space X]
    (hs : IsCompact s) (U : Œπ ‚Üí Set X) (ho : ‚àÄ i, IsOpen (U i)) (hf : LocallyFinite U)
    (hU : s ‚äÜ ‚ãÉ i, U i) :
    ‚àÉ f : PartitionOfUnity Œπ X s, f.IsSubordinate U ‚àß ‚àÄ i, HasCompactSupport (f i) :=
  let ‚ü®f, hfsub, hfcp‚ü© :=
    BumpCovering.exists_isSubordinate_hasCompactSupport_of_locallyFinite_t2space hs U ho hf hU
  ‚ü®f.toPartitionOfUnity, hfsub.toPartitionOfUnity, fun i => IsCompact.of_isClosed_subset (hfcp i)
    isClosed_closure <| closure_mono (f.support_toPartitionOfUnity_subset i)‚ü©

end PartitionOfUnity

/-- A variation of **Urysohn's lemma**.

In a locally compact T2 space `X`, for a compact set `t` and a finite family of open sets `{s i}_i`
such that `t ‚äÜ ‚ãÉ i, s i`, there is a family of compactly supported continuous functions `{f i}_i`
supported in `s i`, `‚àë i, f i x = 1` on `t` and `0 ‚â§ f i x ‚â§ 1`. -/
theorem exists_continuous_sum_one_of_isOpen_isCompact [T2Space X] [LocallyCompactSpace X]
    {n : ‚Ñï} {t : Set X} {s : Fin n ‚Üí Set X} (hs : ‚àÄ (i : Fin n), IsOpen (s i)) (htcp : IsCompact t)
    (hst : t ‚äÜ ‚ãÉ i, s i) :
    ‚àÉ f : Fin n ‚Üí C(X, ‚Ñù), (‚àÄ (i : Fin n), tsupport (f i) ‚äÜ s i) ‚àß EqOn (‚àë i, f i) 1 t
      ‚àß (‚àÄ (i : Fin n), ‚àÄ (x : X), f i x ‚àà Icc (0 : ‚Ñù) 1)
      ‚àß (‚àÄ (i : Fin n), HasCompactSupport (f i)) := by
  obtain ‚ü®f, hfsub, hfcp‚ü© := PartitionOfUnity.exists_isSubordinate_of_locallyFinite_t2space htcp s
    hs (locallyFinite_of_finite _) hst
  use f
  refine ‚ü®fun i ‚Ü¶ hfsub i, ?_, ?_, fun i => hfcp i‚ü©
  ¬∑ intro x hx
    simp only [Finset.sum_apply, Pi.one_apply]
    have h := f.sum_eq_one' x hx
    rw [finsum_eq_sum (fun i => (f.toFun i) x)
      (Finite.subset finite_univ (subset_univ (support fun i ‚Ü¶ (f.toFun i) x)))] at h
    rwa [Fintype.sum_subset (by simp)] at h
  intro i x
  exact ‚ü®f.nonneg i x, PartitionOfUnity.le_one f i x‚ü©
