/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import Mathlib.Topology.UniformSpace.UniformConvergenceTopology

#align_import topology.uniform_space.equicontinuity from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Equicontinuity of a family of functions

Let `X` be a topological space and `Œ±` a `UniformSpace`. A family of functions `F : Œπ ‚Üí X ‚Üí Œ±`
is said to be *equicontinuous at a point `x‚ÇÄ : X`* when, for any entourage `U` in `Œ±`, there is a
neighborhood `V` of `x‚ÇÄ` such that, for all `x ‚àà V`, and *for all `i`*, `F i x` is `U`-close to
`F i x‚ÇÄ`. In other words, one has `‚àÄ U ‚àà ùì§ Œ±, ‚àÄ·∂† x in ùìù x‚ÇÄ, ‚àÄ i, (F i x‚ÇÄ, F i x) ‚àà U`.
For maps between metric spaces, this corresponds to
`‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ x, ‚àÄ i, dist x‚ÇÄ x < Œ¥ ‚Üí dist (F i x‚ÇÄ) (F i x) < Œµ`.

`F` is said to be *equicontinuous* if it is equicontinuous at each point.

A closely related concept is that of ***uniform*** *equicontinuity* of a family of functions
`F : Œπ ‚Üí Œ≤ ‚Üí Œ±` between uniform spaces, which means that, for any entourage `U` in `Œ±`, there is an
entourage `V` in `Œ≤` such that, if `x` and `y` are `V`-close, then *for all `i`*, `F i x` and
`F i y` are `U`-close. In other words, one has
`‚àÄ U ‚àà ùì§ Œ±, ‚àÄ·∂† xy in ùì§ Œ≤, ‚àÄ i, (F i xy.1, F i xy.2) ‚àà U`.
For maps between metric spaces, this corresponds to
`‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ x y, ‚àÄ i, dist x y < Œ¥ ‚Üí dist (F i x‚ÇÄ) (F i x) < Œµ`.

## Main definitions

* `EquicontinuousAt`: equicontinuity of a family of functions at a point
* `Equicontinuous`: equicontinuity of a family of functions on the whole domain
* `UniformEquicontinuous`: uniform equicontinuity of a family of functions on the whole domain

## Main statements

* `equicontinuous_iff_continuous`: equicontinuity can be expressed as a simple continuity
  condition between well-chosen function spaces. This is really useful for building up the theory.
* `Equicontinuous.closure`: if a set of functions is equicontinuous, its closure
  *for the topology of uniform convergence* is also equicontinuous.

## Notations

Throughout this file, we use :
- `Œπ`, `Œ∫` for indexing types
- `X`, `Y`, `Z` for topological spaces
- `Œ±`, `Œ≤`, `Œ≥` for uniform spaces

## Implementation details

We choose to express equicontinuity as a properties of indexed families of functions rather
than sets of functions for the following reasons:
- it is really easy to express equicontinuity of `H : Set (X ‚Üí Œ±)` using our setup: it is just
  equicontinuity of the family `(‚Üë) : ‚Ü•H ‚Üí (X ‚Üí Œ±)`. On the other hand, going the other way around
  would require working with the range of the family, which is always annoying because it
  introduces useless existentials.
- in most applications, one doesn't work with bare functions but with a more specific hom type
  `hom`. Equicontinuity of a set `H : Set hom` would then have to be expressed as equicontinuity
  of `coe_fn '' H`, which is super annoying to work with. This is much simpler with families,
  because equicontinuity of a family `ùìï : Œπ ‚Üí hom` would simply be expressed as equicontinuity
  of `coe_fn ‚àò ùìï`, which doesn't introduce any nasty existentials.

To simplify statements, we do provide abbreviations `Set.EquicontinuousAt`, `Set.Equicontinuous`
and `Set.UniformEquicontinuous` asserting the corresponding fact about the family
`(‚Üë) : ‚Ü•H ‚Üí (X ‚Üí Œ±)` where `H : Set (X ‚Üí Œ±)`. Note however that these won't work for sets of hom
types, and in that case one should go back to the family definition rather than using `Set.image`.

Since we have no use case for it yet, we don't introduce any relative version
(i.e no `EquicontinuousWithinAt` or `EquicontinuousOn`), but this is more of a conservative
position than a design decision, so anyone needing relative versions should feel free to add them,
and that should hopefully be a straightforward task.

## References

* [N. Bourbaki, *General Topology, Chapter X*][bourbaki1966]

## Tags

equicontinuity, uniform convergence, ascoli
-/


section

open UniformSpace Filter Set Uniformity Topology UniformConvergence

variable {Œπ Œ∫ X Y Z Œ± Œ≤ Œ≥ ùìï : Type*} [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]
  [UniformSpace Œ±] [UniformSpace Œ≤] [UniformSpace Œ≥]

/-- A family `F : Œπ ‚Üí X ‚Üí Œ±` of functions from a topological space to a uniform space is
*equicontinuous at `x‚ÇÄ : X`* if, for all entourage `U ‚àà ùì§ Œ±`, there is a neighborhood `V` of `x‚ÇÄ`
such that, for all `x ‚àà V` and for all `i : Œπ`, `F i x` is `U`-close to `F i x‚ÇÄ`. -/
def EquicontinuousAt (F : Œπ ‚Üí X ‚Üí Œ±) (x‚ÇÄ : X) : Prop :=
  ‚àÄ U ‚àà ùì§ Œ±, ‚àÄ·∂† x in ùìù x‚ÇÄ, ‚àÄ i, (F i x‚ÇÄ, F i x) ‚àà U
#align equicontinuous_at EquicontinuousAt

/-- We say that a set `H : Set (X ‚Üí Œ±)` of functions is equicontinuous at a point if the family
`(‚Üë) : ‚Ü•H ‚Üí (X ‚Üí Œ±)` is equicontinuous at that point. -/
protected abbrev Set.EquicontinuousAt (H : Set <| X ‚Üí Œ±) (x‚ÇÄ : X) : Prop :=
  EquicontinuousAt ((‚Üë) : H ‚Üí X ‚Üí Œ±) x‚ÇÄ
#align set.equicontinuous_at Set.EquicontinuousAt

/-- A family `F : Œπ ‚Üí X ‚Üí Œ±` of functions from a topological space to a uniform space is
*equicontinuous* on all of `X` if it is equicontinuous at each point of `X`. -/
def Equicontinuous (F : Œπ ‚Üí X ‚Üí Œ±) : Prop :=
  ‚àÄ x‚ÇÄ, EquicontinuousAt F x‚ÇÄ
#align equicontinuous Equicontinuous

/-- We say that a set `H : Set (X ‚Üí Œ±)` of functions is equicontinuous if the family
`(‚Üë) : ‚Ü•H ‚Üí (X ‚Üí Œ±)` is equicontinuous. -/
protected abbrev Set.Equicontinuous (H : Set <| X ‚Üí Œ±) : Prop :=
  Equicontinuous ((‚Üë) : H ‚Üí X ‚Üí Œ±)
#align set.equicontinuous Set.Equicontinuous

/-- A family `F : Œπ ‚Üí Œ≤ ‚Üí Œ±` of functions between uniform spaces is *uniformly equicontinuous* if,
for all entourage `U ‚àà ùì§ Œ±`, there is an entourage `V ‚àà ùì§ Œ≤` such that, whenever `x` and `y` are
`V`-close, we have that, *for all `i : Œπ`*, `F i x` is `U`-close to `F i x‚ÇÄ`. -/
def UniformEquicontinuous (F : Œπ ‚Üí Œ≤ ‚Üí Œ±) : Prop :=
  ‚àÄ U ‚àà ùì§ Œ±, ‚àÄ·∂† xy : Œ≤ √ó Œ≤ in ùì§ Œ≤, ‚àÄ i, (F i xy.1, F i xy.2) ‚àà U
#align uniform_equicontinuous UniformEquicontinuous

/-- We say that a set `H : Set (X ‚Üí Œ±)` of functions is uniformly equicontinuous if the family
`(‚Üë) : ‚Ü•H ‚Üí (X ‚Üí Œ±)` is uniformly equicontinuous. -/
protected abbrev Set.UniformEquicontinuous (H : Set <| Œ≤ ‚Üí Œ±) : Prop :=
  UniformEquicontinuous ((‚Üë) : H ‚Üí Œ≤ ‚Üí Œ±)
#align set.uniform_equicontinuous Set.UniformEquicontinuous

/-!
### Empty index type
-/

@[simp]
lemma equicontinuousAt_empty [h : IsEmpty Œπ] (F : Œπ ‚Üí X ‚Üí Œ±) (x‚ÇÄ : X) :
    EquicontinuousAt F x‚ÇÄ :=
  fun _ _ ‚Ü¶ eventually_of_forall (fun _ ‚Ü¶ h.elim)

@[simp]
lemma equicontinuous_empty [IsEmpty Œπ] (F : Œπ ‚Üí X ‚Üí Œ±) :
    Equicontinuous F :=
  equicontinuousAt_empty F

@[simp]
lemma uniformEquicontinuous_empty [h : IsEmpty Œπ] (F : Œπ ‚Üí Œ≤ ‚Üí Œ±) :
    UniformEquicontinuous F :=
  fun _ _ ‚Ü¶ eventually_of_forall (fun _ ‚Ü¶ h.elim)

/-!
### Finite index type
-/

theorem equicontinuousAt_finite [Finite Œπ] {F : Œπ ‚Üí X ‚Üí Œ±} {x‚ÇÄ : X} :
    EquicontinuousAt F x‚ÇÄ ‚Üî ‚àÄ i, ContinuousAt (F i) x‚ÇÄ := by
  simp [EquicontinuousAt, ContinuousAt, (nhds_basis_uniformity' (ùì§ Œ±).basis_sets).tendsto_right_iff,
    UniformSpace.ball, @forall_swap _ Œπ]

theorem equicontinuous_finite [Finite Œπ] {F : Œπ ‚Üí X ‚Üí Œ±} :
    Equicontinuous F ‚Üî ‚àÄ i, Continuous (F i) := by
  simp only [Equicontinuous, equicontinuousAt_finite, continuous_iff_continuousAt, @forall_swap Œπ]

theorem uniformEquicontinuous_finite [Finite Œπ] {F : Œπ ‚Üí Œ≤ ‚Üí Œ±} :
    UniformEquicontinuous F ‚Üî ‚àÄ i, UniformContinuous (F i) := by
  simp only [UniformEquicontinuous, eventually_all, @forall_swap _ Œπ]; rfl

/-!
### Index type with a unique element
-/

theorem equicontinuousAt_unique [Unique Œπ] {F : Œπ ‚Üí X ‚Üí Œ±} {x : X} :
    EquicontinuousAt F x ‚Üî ContinuousAt (F default) x :=
  equicontinuousAt_finite.trans Unique.forall_iff

theorem equicontinuous_unique [Unique Œπ] {F : Œπ ‚Üí X ‚Üí Œ±} :
    Equicontinuous F ‚Üî Continuous (F default) :=
  equicontinuous_finite.trans Unique.forall_iff

theorem uniformEquicontinuous_unique [Unique Œπ] {F : Œπ ‚Üí Œ≤ ‚Üí Œ±} :
    UniformEquicontinuous F ‚Üî UniformContinuous (F default) :=
  uniformEquicontinuous_finite.trans Unique.forall_iff

/-- Reformulation of equicontinuity at `x‚ÇÄ` comparing two variables near `x‚ÇÄ` instead of comparing
only one with `x‚ÇÄ`. -/
theorem equicontinuousAt_iff_pair {F : Œπ ‚Üí X ‚Üí Œ±} {x‚ÇÄ : X} :
    EquicontinuousAt F x‚ÇÄ ‚Üî
      ‚àÄ U ‚àà ùì§ Œ±, ‚àÉ V ‚àà ùìù x‚ÇÄ, ‚àÄ x ‚àà V, ‚àÄ y ‚àà V, ‚àÄ i, (F i x, F i y) ‚àà U := by
  constructor <;> intro H U hU
  ¬∑ rcases comp_symm_mem_uniformity_sets hU with ‚ü®V, hV, hVsymm, hVU‚ü©
    refine' ‚ü®_, H V hV, fun x hx y hy i => hVU (prod_mk_mem_compRel _ (hy i))‚ü©
    exact hVsymm.mk_mem_comm.mp (hx i)
  ¬∑ rcases H U hU with ‚ü®V, hV, hVU‚ü©
    filter_upwards [hV] using fun x hx i => hVU x‚ÇÄ (mem_of_mem_nhds hV) x hx i
#align equicontinuous_at_iff_pair equicontinuousAt_iff_pair

/-- Uniform equicontinuity implies equicontinuity. -/
theorem UniformEquicontinuous.equicontinuous {F : Œπ ‚Üí Œ≤ ‚Üí Œ±} (h : UniformEquicontinuous F) :
    Equicontinuous F := fun x‚ÇÄ U hU =>
  mem_of_superset (ball_mem_nhds x‚ÇÄ (h U hU)) fun _ hx i => hx i
#align uniform_equicontinuous.equicontinuous UniformEquicontinuous.equicontinuous

/-- Each function of a family equicontinuous at `x‚ÇÄ` is continuous at `x‚ÇÄ`. -/
theorem EquicontinuousAt.continuousAt {F : Œπ ‚Üí X ‚Üí Œ±} {x‚ÇÄ : X} (h : EquicontinuousAt F x‚ÇÄ) (i : Œπ) :
    ContinuousAt (F i) x‚ÇÄ :=
  (UniformSpace.hasBasis_nhds _).tendsto_right_iff.2 fun U ‚ü®hU, _‚ü© ‚Ü¶ (h U hU).mono fun _x hx ‚Ü¶ hx i
#align equicontinuous_at.continuous_at EquicontinuousAt.continuousAt

protected theorem Set.EquicontinuousAt.continuousAt_of_mem {H : Set <| X ‚Üí Œ±} {x‚ÇÄ : X}
    (h : H.EquicontinuousAt x‚ÇÄ) {f : X ‚Üí Œ±} (hf : f ‚àà H) : ContinuousAt f x‚ÇÄ :=
  h.continuousAt ‚ü®f, hf‚ü©
#align set.equicontinuous_at.continuous_at_of_mem Set.EquicontinuousAt.continuousAt_of_mem

/-- Each function of an equicontinuous family is continuous. -/
theorem Equicontinuous.continuous {F : Œπ ‚Üí X ‚Üí Œ±} (h : Equicontinuous F) (i : Œπ) :
    Continuous (F i) :=
  continuous_iff_continuousAt.mpr fun x => (h x).continuousAt i
#align equicontinuous.continuous Equicontinuous.continuous

protected theorem Set.Equicontinuous.continuous_of_mem {H : Set <| X ‚Üí Œ±} (h : H.Equicontinuous)
    {f : X ‚Üí Œ±} (hf : f ‚àà H) : Continuous f :=
  h.continuous ‚ü®f, hf‚ü©
#align set.equicontinuous.continuous_of_mem Set.Equicontinuous.continuous_of_mem

/-- Each function of a uniformly equicontinuous family is uniformly continuous. -/
theorem UniformEquicontinuous.uniformContinuous {F : Œπ ‚Üí Œ≤ ‚Üí Œ±} (h : UniformEquicontinuous F)
    (i : Œπ) : UniformContinuous (F i) := fun U hU =>
  mem_map.mpr (mem_of_superset (h U hU) fun _ hxy => hxy i)
#align uniform_equicontinuous.uniform_continuous UniformEquicontinuous.uniformContinuous

protected theorem Set.UniformEquicontinuous.uniformContinuous_of_mem {H : Set <| Œ≤ ‚Üí Œ±}
    (h : H.UniformEquicontinuous) {f : Œ≤ ‚Üí Œ±} (hf : f ‚àà H) : UniformContinuous f :=
  h.uniformContinuous ‚ü®f, hf‚ü©
#align set.uniform_equicontinuous.uniform_continuous_of_mem Set.UniformEquicontinuous.uniformContinuous_of_mem

/-- Taking sub-families preserves equicontinuity at a point. -/
theorem EquicontinuousAt.comp {F : Œπ ‚Üí X ‚Üí Œ±} {x‚ÇÄ : X} (h : EquicontinuousAt F x‚ÇÄ) (u : Œ∫ ‚Üí Œπ) :
    EquicontinuousAt (F ‚àò u) x‚ÇÄ := fun U hU => (h U hU).mono fun _ H k => H (u k)
#align equicontinuous_at.comp EquicontinuousAt.comp

protected theorem Set.EquicontinuousAt.mono {H H' : Set <| X ‚Üí Œ±} {x‚ÇÄ : X}
    (h : H.EquicontinuousAt x‚ÇÄ) (hH : H' ‚äÜ H) : H'.EquicontinuousAt x‚ÇÄ :=
  h.comp (inclusion hH)
#align set.equicontinuous_at.mono Set.EquicontinuousAt.mono

/-- Taking sub-families preserves equicontinuity. -/
theorem Equicontinuous.comp {F : Œπ ‚Üí X ‚Üí Œ±} (h : Equicontinuous F) (u : Œ∫ ‚Üí Œπ) :
    Equicontinuous (F ‚àò u) := fun x => (h x).comp u
#align equicontinuous.comp Equicontinuous.comp

protected theorem Set.Equicontinuous.mono {H H' : Set <| X ‚Üí Œ±} (h : H.Equicontinuous)
    (hH : H' ‚äÜ H) : H'.Equicontinuous :=
  h.comp (inclusion hH)
#align set.equicontinuous.mono Set.Equicontinuous.mono

/-- Taking sub-families preserves uniform equicontinuity. -/
theorem UniformEquicontinuous.comp {F : Œπ ‚Üí Œ≤ ‚Üí Œ±} (h : UniformEquicontinuous F) (u : Œ∫ ‚Üí Œπ) :
    UniformEquicontinuous (F ‚àò u) := fun U hU => (h U hU).mono fun _ H k => H (u k)
#align uniform_equicontinuous.comp UniformEquicontinuous.comp

protected theorem Set.UniformEquicontinuous.mono {H H' : Set <| Œ≤ ‚Üí Œ±} (h : H.UniformEquicontinuous)
    (hH : H' ‚äÜ H) : H'.UniformEquicontinuous :=
  h.comp (inclusion hH)
#align set.uniform_equicontinuous.mono Set.UniformEquicontinuous.mono

/-- A family `ùìï : Œπ ‚Üí X ‚Üí Œ±` is equicontinuous at `x‚ÇÄ` iff `range ùìï` is equicontinuous at `x‚ÇÄ`,
i.e the family `(‚Üë) : range F ‚Üí X ‚Üí Œ±` is equicontinuous at `x‚ÇÄ`. -/
theorem equicontinuousAt_iff_range {F : Œπ ‚Üí X ‚Üí Œ±} {x‚ÇÄ : X} :
    EquicontinuousAt F x‚ÇÄ ‚Üî EquicontinuousAt ((‚Üë) : range F ‚Üí X ‚Üí Œ±) x‚ÇÄ := by
  simp only [EquicontinuousAt, forall_subtype_range_iff]
#align equicontinuous_at_iff_range equicontinuousAt_iff_range

/-- A family `ùìï : Œπ ‚Üí X ‚Üí Œ±` is equicontinuous iff `range ùìï` is equicontinuous,
i.e the family `(‚Üë) : range F ‚Üí X ‚Üí Œ±` is equicontinuous. -/
theorem equicontinuous_iff_range {F : Œπ ‚Üí X ‚Üí Œ±} :
    Equicontinuous F ‚Üî Equicontinuous ((‚Üë) : range F ‚Üí X ‚Üí Œ±) :=
  forall_congr' fun _ => equicontinuousAt_iff_range
#align equicontinuous_iff_range equicontinuous_iff_range

/-- A family `ùìï : Œπ ‚Üí Œ≤ ‚Üí Œ±` is uniformly equicontinuous iff `range ùìï` is uniformly equicontinuous,
i.e the family `(‚Üë) : range F ‚Üí Œ≤ ‚Üí Œ±` is uniformly equicontinuous. -/
theorem uniformEquicontinuous_at_iff_range {F : Œπ ‚Üí Œ≤ ‚Üí Œ±} :
    UniformEquicontinuous F ‚Üî UniformEquicontinuous ((‚Üë) : range F ‚Üí Œ≤ ‚Üí Œ±) :=
  ‚ü®fun h => by rw [‚Üê comp_rangeSplitting F]; exact h.comp _, fun h =>
    h.comp (rangeFactorization F)‚ü©
#align uniform_equicontinuous_at_iff_range uniformEquicontinuous_at_iff_range

section

open UniformFun

/-- A family `ùìï : Œπ ‚Üí X ‚Üí Œ±` is equicontinuous at `x‚ÇÄ` iff the function `swap ùìï : X ‚Üí Œπ ‚Üí Œ±` is
continuous at `x‚ÇÄ` *when `Œπ ‚Üí Œ±` is equipped with the topology of uniform convergence*. This is
very useful for developping the equicontinuity API, but it should not be used directly for other
purposes. -/
theorem equicontinuousAt_iff_continuousAt {F : Œπ ‚Üí X ‚Üí Œ±} {x‚ÇÄ : X} :
    EquicontinuousAt F x‚ÇÄ ‚Üî ContinuousAt (ofFun ‚àò Function.swap F : X ‚Üí Œπ ‚Üí·µ§ Œ±) x‚ÇÄ := by
  rw [ContinuousAt, (UniformFun.hasBasis_nhds Œπ Œ± _).tendsto_right_iff]
  rfl
#align equicontinuous_at_iff_continuous_at equicontinuousAt_iff_continuousAt

/-- A family `ùìï : Œπ ‚Üí X ‚Üí Œ±` is equicontinuous iff the function `swap ùìï : X ‚Üí Œπ ‚Üí Œ±` is
continuous *when `Œπ ‚Üí Œ±` is equipped with the topology of uniform convergence*. This is
very useful for developping the equicontinuity API, but it should not be used directly for other
purposes. -/
theorem equicontinuous_iff_continuous {F : Œπ ‚Üí X ‚Üí Œ±} :
    Equicontinuous F ‚Üî Continuous (ofFun ‚àò Function.swap F : X ‚Üí Œπ ‚Üí·µ§ Œ±) := by
  simp_rw [Equicontinuous, continuous_iff_continuousAt, equicontinuousAt_iff_continuousAt]
#align equicontinuous_iff_continuous equicontinuous_iff_continuous

/-- A family `ùìï : Œπ ‚Üí Œ≤ ‚Üí Œ±` is uniformly equicontinuous iff the function `swap ùìï : Œ≤ ‚Üí Œπ ‚Üí Œ±` is
uniformly continuous *when `Œπ ‚Üí Œ±` is equipped with the uniform structure of uniform convergence*.
This is very useful for developping the equicontinuity API, but it should not be used directly
for other purposes. -/
theorem uniformEquicontinuous_iff_uniformContinuous {F : Œπ ‚Üí Œ≤ ‚Üí Œ±} :
    UniformEquicontinuous F ‚Üî UniformContinuous (ofFun ‚àò Function.swap F : Œ≤ ‚Üí Œπ ‚Üí·µ§ Œ±) := by
  rw [UniformContinuous, (UniformFun.hasBasis_uniformity Œπ Œ±).tendsto_right_iff]
  rfl
#align uniform_equicontinuous_iff_uniform_continuous uniformEquicontinuous_iff_uniformContinuous

theorem equicontinuousAt_iInf_rng {Œ±' : Type*} {u : Œ∫ ‚Üí UniformSpace Œ±'} {F : Œπ ‚Üí X ‚Üí Œ±'}
    {x‚ÇÄ : X} :
    @EquicontinuousAt _ _ _ _ (‚®Ö k, u k) F x‚ÇÄ ‚Üî ‚àÄ k, @EquicontinuousAt _ _ _ _ (u k) F x‚ÇÄ := by
  simp only [@equicontinuousAt_iff_continuousAt _ _ _ _ _, topologicalSpace]
  unfold ContinuousAt
  rw [UniformFun.iInf_eq, toTopologicalSpace_iInf, nhds_iInf, tendsto_iInf]

theorem equicontinuous_iInf_rng {Œ±' : Type*} {u : Œ∫ ‚Üí UniformSpace Œ±'} {F : Œπ ‚Üí X ‚Üí Œ±'} :
    @Equicontinuous _ _ _ _ (‚®Ö k, u k) F ‚Üî ‚àÄ k, @Equicontinuous _ _ _ _ (u k) F := by
  simp_rw [@equicontinuous_iff_continuous _ _ _ _ _, UniformFun.topologicalSpace]
  rw [UniformFun.iInf_eq, toTopologicalSpace_iInf, continuous_iInf_rng]

theorem uniformEquicontinuous_iInf_rng {Œ±' : Type*} {u : Œ∫ ‚Üí UniformSpace Œ±'} {F : Œπ ‚Üí Œ≤ ‚Üí Œ±'} :
    @UniformEquicontinuous _ _ _ (‚®Ö k, u k) _ F ‚Üî ‚àÄ k, @UniformEquicontinuous _ _ _ (u k) _ F := by
  simp_rw [@uniformEquicontinuous_iff_uniformContinuous _ _ _ _]
  rw [UniformFun.iInf_eq, uniformContinuous_iInf_rng]

theorem equicontinuousAt_iInf_dom {X' : Type*} {t : Œ∫ ‚Üí TopologicalSpace X'} {F : Œπ ‚Üí X' ‚Üí Œ±}
    {x‚ÇÄ : X'} {k : Œ∫} (hk : @EquicontinuousAt _ _ _ (t k) _ F x‚ÇÄ) :
    @EquicontinuousAt _ _ _ (‚®Ö k, t k) _ F x‚ÇÄ := by
  simp [@equicontinuousAt_iff_continuousAt _ _ _ _] at hk ‚ä¢
  unfold ContinuousAt at hk ‚ä¢
  rw [nhds_iInf]
  exact tendsto_iInf' k hk

theorem equicontinuous_iInf_dom {X' : Type*} {t : Œ∫ ‚Üí TopologicalSpace X'} {F : Œπ ‚Üí X' ‚Üí Œ±}
    {k : Œ∫} (hk : @Equicontinuous _ _ _ (t k) _ F) :
    @Equicontinuous _ _ _ (‚®Ö k, t k) _ F := by
  simp_rw [@equicontinuous_iff_continuous _ _ _ _] at hk ‚ä¢
  exact continuous_iInf_dom hk

theorem uniform_equicontinuous_infi_dom {Œ≤' : Type*} {u : Œ∫ ‚Üí UniformSpace Œ≤'} {F : Œπ ‚Üí Œ≤' ‚Üí Œ±}
    {k : Œ∫} (hk : @UniformEquicontinuous _ _ _ _ (u k) F) :
    @UniformEquicontinuous _ _ _ _ (‚®Ö k, u k) F := by
  simp_rw [@uniformEquicontinuous_iff_uniformContinuous _ _ _ _ _] at hk ‚ä¢
  exact uniformContinuous_iInf_dom hk

-- Porting note: changed from `‚àÉ k (_ : p k), _` to `‚àÉ k, p k ‚àß _` since Lean 4 generates the
-- second one when parsing expressions like `‚àÉ Œ¥ > 0, _`.
theorem Filter.HasBasis.equicontinuousAt_iff_left {Œ∫ : Type*} {p : Œ∫ ‚Üí Prop} {s : Œ∫ ‚Üí Set X}
    {F : Œπ ‚Üí X ‚Üí Œ±} {x‚ÇÄ : X} (hX : (ùìù x‚ÇÄ).HasBasis p s) :
    EquicontinuousAt F x‚ÇÄ ‚Üî ‚àÄ U ‚àà ùì§ Œ±, ‚àÉ k, p k ‚àß ‚àÄ x ‚àà s k, ‚àÄ i, (F i x‚ÇÄ, F i x) ‚àà U := by
  rw [equicontinuousAt_iff_continuousAt, ContinuousAt,
    hX.tendsto_iff (UniformFun.hasBasis_nhds Œπ Œ± _)]
  simp only [Function.comp_apply, mem_setOf_eq, exists_prop]
  rfl
#align filter.has_basis.equicontinuous_at_iff_left Filter.HasBasis.equicontinuousAt_iff_left

theorem Filter.HasBasis.equicontinuousAt_iff_right {Œ∫ : Type*} {p : Œ∫ ‚Üí Prop} {s : Œ∫ ‚Üí Set (Œ± √ó Œ±)}
    {F : Œπ ‚Üí X ‚Üí Œ±} {x‚ÇÄ : X} (hŒ± : (ùì§ Œ±).HasBasis p s) :
    EquicontinuousAt F x‚ÇÄ ‚Üî ‚àÄ k, p k ‚Üí ‚àÄ·∂† x in ùìù x‚ÇÄ, ‚àÄ i, (F i x‚ÇÄ, F i x) ‚àà s k := by
  rw [equicontinuousAt_iff_continuousAt, ContinuousAt,
    (UniformFun.hasBasis_nhds_of_basis Œπ Œ± _ hŒ±).tendsto_right_iff]
  rfl
#align filter.has_basis.equicontinuous_at_iff_right Filter.HasBasis.equicontinuousAt_iff_right

-- Porting note: changed from `‚àÉ k (_ : p k), _` to `‚àÉ k, p k ‚àß _` since Lean 4 generates the
-- second one when parsing expressions like `‚àÉ Œ¥ > 0, _`.
theorem Filter.HasBasis.equicontinuousAt_iff {Œ∫‚ÇÅ Œ∫‚ÇÇ : Type*} {p‚ÇÅ : Œ∫‚ÇÅ ‚Üí Prop} {s‚ÇÅ : Œ∫‚ÇÅ ‚Üí Set X}
    {p‚ÇÇ : Œ∫‚ÇÇ ‚Üí Prop} {s‚ÇÇ : Œ∫‚ÇÇ ‚Üí Set (Œ± √ó Œ±)} {F : Œπ ‚Üí X ‚Üí Œ±} {x‚ÇÄ : X} (hX : (ùìù x‚ÇÄ).HasBasis p‚ÇÅ s‚ÇÅ)
    (hŒ± : (ùì§ Œ±).HasBasis p‚ÇÇ s‚ÇÇ) :
    EquicontinuousAt F x‚ÇÄ ‚Üî
      ‚àÄ k‚ÇÇ, p‚ÇÇ k‚ÇÇ ‚Üí ‚àÉ k‚ÇÅ, p‚ÇÅ k‚ÇÅ ‚àß ‚àÄ x ‚àà s‚ÇÅ k‚ÇÅ, ‚àÄ i, (F i x‚ÇÄ, F i x) ‚àà s‚ÇÇ k‚ÇÇ := by
  rw [equicontinuousAt_iff_continuousAt, ContinuousAt,
    hX.tendsto_iff (UniformFun.hasBasis_nhds_of_basis Œπ Œ± _ hŒ±)]
  simp only [Function.comp_apply, mem_setOf_eq, exists_prop]
  rfl
#align filter.has_basis.equicontinuous_at_iff Filter.HasBasis.equicontinuousAt_iff

-- Porting note: changed from `‚àÉ k (_ : p k), _` to `‚àÉ k, p k ‚àß _` since Lean 4 generates the
-- second one when parsing expressions like `‚àÉ Œ¥ > 0, _`.
theorem Filter.HasBasis.uniformEquicontinuous_iff_left {Œ∫ : Type*} {p : Œ∫ ‚Üí Prop}
    {s : Œ∫ ‚Üí Set (Œ≤ √ó Œ≤)} {F : Œπ ‚Üí Œ≤ ‚Üí Œ±} (hŒ≤ : (ùì§ Œ≤).HasBasis p s) :
    UniformEquicontinuous F ‚Üî
      ‚àÄ U ‚àà ùì§ Œ±, ‚àÉ k, p k ‚àß ‚àÄ x y, (x, y) ‚àà s k ‚Üí ‚àÄ i, (F i x, F i y) ‚àà U := by
  rw [uniformEquicontinuous_iff_uniformContinuous, UniformContinuous,
    hŒ≤.tendsto_iff (UniformFun.hasBasis_uniformity Œπ Œ±)]
  simp only [Prod.forall, Function.comp_apply, mem_setOf_eq, exists_prop]
  rfl
#align filter.has_basis.uniform_equicontinuous_iff_left Filter.HasBasis.uniformEquicontinuous_iff_left

theorem Filter.HasBasis.uniformEquicontinuous_iff_right {Œ∫ : Type*} {p : Œ∫ ‚Üí Prop}
    {s : Œ∫ ‚Üí Set (Œ± √ó Œ±)} {F : Œπ ‚Üí Œ≤ ‚Üí Œ±} (hŒ± : (ùì§ Œ±).HasBasis p s) :
    UniformEquicontinuous F ‚Üî ‚àÄ k, p k ‚Üí ‚àÄ·∂† xy : Œ≤ √ó Œ≤ in ùì§ Œ≤, ‚àÄ i, (F i xy.1, F i xy.2) ‚àà s k := by
  rw [uniformEquicontinuous_iff_uniformContinuous, UniformContinuous,
    (UniformFun.hasBasis_uniformity_of_basis Œπ Œ± hŒ±).tendsto_right_iff]
  rfl
#align filter.has_basis.uniform_equicontinuous_iff_right Filter.HasBasis.uniformEquicontinuous_iff_right

-- Porting note: changed from `‚àÉ k (_ : p k), _` to `‚àÉ k, p k ‚àß _` since Lean 4 generates the
-- second one when parsing expressions like `‚àÉ Œ¥ > 0, _`.
theorem Filter.HasBasis.uniformEquicontinuous_iff {Œ∫‚ÇÅ Œ∫‚ÇÇ : Type*} {p‚ÇÅ : Œ∫‚ÇÅ ‚Üí Prop}
    {s‚ÇÅ : Œ∫‚ÇÅ ‚Üí Set (Œ≤ √ó Œ≤)} {p‚ÇÇ : Œ∫‚ÇÇ ‚Üí Prop} {s‚ÇÇ : Œ∫‚ÇÇ ‚Üí Set (Œ± √ó Œ±)} {F : Œπ ‚Üí Œ≤ ‚Üí Œ±}
    (hŒ≤ : (ùì§ Œ≤).HasBasis p‚ÇÅ s‚ÇÅ) (hŒ± : (ùì§ Œ±).HasBasis p‚ÇÇ s‚ÇÇ) :
    UniformEquicontinuous F ‚Üî
      ‚àÄ k‚ÇÇ, p‚ÇÇ k‚ÇÇ ‚Üí ‚àÉ k‚ÇÅ, p‚ÇÅ k‚ÇÅ ‚àß ‚àÄ x y, (x, y) ‚àà s‚ÇÅ k‚ÇÅ ‚Üí ‚àÄ i, (F i x, F i y) ‚àà s‚ÇÇ k‚ÇÇ := by
  rw [uniformEquicontinuous_iff_uniformContinuous, UniformContinuous,
    hŒ≤.tendsto_iff (UniformFun.hasBasis_uniformity_of_basis Œπ Œ± hŒ±)]
  simp only [Prod.forall, Function.comp_apply, mem_setOf_eq, exists_prop]
  rfl
#align filter.has_basis.uniform_equicontinuous_iff Filter.HasBasis.uniformEquicontinuous_iff

/-- Given `u : Œ± ‚Üí Œ≤` a uniform inducing map, a family `ùìï : Œπ ‚Üí X ‚Üí Œ±` is equicontinuous at a point
`x‚ÇÄ : X` iff the family `ùìï'`, obtained by precomposing each function of `ùìï` by `u`, is
equicontinuous at `x‚ÇÄ`. -/
theorem UniformInducing.equicontinuousAt_iff {F : Œπ ‚Üí X ‚Üí Œ±} {x‚ÇÄ : X} {u : Œ± ‚Üí Œ≤}
    (hu : UniformInducing u) : EquicontinuousAt F x‚ÇÄ ‚Üî EquicontinuousAt ((¬∑ ‚àò ¬∑) u ‚àò F) x‚ÇÄ := by
  have := (UniformFun.postcomp_uniformInducing (Œ± := Œπ) hu).inducing
  rw [equicontinuousAt_iff_continuousAt, equicontinuousAt_iff_continuousAt, this.continuousAt_iff]
  rfl
#align uniform_inducing.equicontinuous_at_iff UniformInducing.equicontinuousAt_iff

/-- Given `u : Œ± ‚Üí Œ≤` a uniform inducing map, a family `ùìï : Œπ ‚Üí X ‚Üí Œ±` is equicontinuous iff the
family `ùìï'`, obtained by precomposing each function of `ùìï` by `u`, is equicontinuous. -/
theorem UniformInducing.equicontinuous_iff {F : Œπ ‚Üí X ‚Üí Œ±} {u : Œ± ‚Üí Œ≤} (hu : UniformInducing u) :
    Equicontinuous F ‚Üî Equicontinuous ((¬∑ ‚àò ¬∑) u ‚àò F) := by
  congrm ‚àÄ x, ?_
  rw [hu.equicontinuousAt_iff]
#align uniform_inducing.equicontinuous_iff UniformInducing.equicontinuous_iff

/-- Given `u : Œ± ‚Üí Œ≥` a uniform inducing map, a family `ùìï : Œπ ‚Üí Œ≤ ‚Üí Œ±` is uniformly equicontinuous
iff the family `ùìï'`, obtained by precomposing each function of `ùìï` by `u`, is uniformly
equicontinuous. -/
theorem UniformInducing.uniformEquicontinuous_iff {F : Œπ ‚Üí Œ≤ ‚Üí Œ±} {u : Œ± ‚Üí Œ≥}
    (hu : UniformInducing u) : UniformEquicontinuous F ‚Üî UniformEquicontinuous ((¬∑ ‚àò ¬∑) u ‚àò F) := by
  have := UniformFun.postcomp_uniformInducing (Œ± := Œπ) hu
  rw [uniformEquicontinuous_iff_uniformContinuous, uniformEquicontinuous_iff_uniformContinuous,
    this.uniformContinuous_iff]
  rfl
#align uniform_inducing.uniform_equicontinuous_iff UniformInducing.uniformEquicontinuous_iff

/-- A version of `EquicontinuousAt.closure` applicable to subsets of types which embed continuously
into `X ‚Üí Œ±` with the product topology. It turns out we don't need any other condition on the
embedding than continuity, but in practice this will mostly be applied to `FunLike` types where
the coercion is injective. -/
theorem EquicontinuousAt.closure' {A : Set Y} {u : Y ‚Üí X ‚Üí Œ±} {x‚ÇÄ : X}
    (hA : EquicontinuousAt (u ‚àò (‚Üë) : A ‚Üí X ‚Üí Œ±) x‚ÇÄ) (hu : Continuous u) :
    EquicontinuousAt (u ‚àò (‚Üë) : closure A ‚Üí X ‚Üí Œ±) x‚ÇÄ := by
  intro U hU
  rcases mem_uniformity_isClosed hU with ‚ü®V, hV, hVclosed, hVU‚ü©
  filter_upwards [hA V hV] with x hx
  rw [SetCoe.forall] at *
  change A ‚äÜ (fun f => (u f x‚ÇÄ, u f x)) ‚Åª¬π' V at hx
  refine' (closure_minimal hx <| hVclosed.preimage <| _).trans (preimage_mono hVU)
  exact Continuous.prod_mk ((continuous_apply x‚ÇÄ).comp hu) ((continuous_apply x).comp hu)
#align equicontinuous_at.closure' EquicontinuousAt.closure'

/-- If a set of functions is equicontinuous at some `x‚ÇÄ`, its closure for the product topology is
also equicontinuous at `x‚ÇÄ`. -/
protected theorem EquicontinuousAt.closure {A : Set (X ‚Üí Œ±)} {x‚ÇÄ : X} (hA : A.EquicontinuousAt x‚ÇÄ) :
    (closure A).EquicontinuousAt x‚ÇÄ :=
  EquicontinuousAt.closure' (u := id) hA continuous_id
#align equicontinuous_at.closure EquicontinuousAt.closure

/-- If `ùìï : Œπ ‚Üí X ‚Üí Œ±` tends to `f : X ‚Üí Œ±` *pointwise* along some nontrivial filter, and if the
family `ùìï` is equicontinuous at some `x‚ÇÄ : X`, then the limit is continuous at `x‚ÇÄ`. -/
theorem Filter.Tendsto.continuousAt_of_equicontinuousAt {l : Filter Œπ} [l.NeBot] {F : Œπ ‚Üí X ‚Üí Œ±}
    {f : X ‚Üí Œ±} {x‚ÇÄ : X} (h‚ÇÅ : Tendsto F l (ùìù f)) (h‚ÇÇ : EquicontinuousAt F x‚ÇÄ) :
    ContinuousAt f x‚ÇÄ :=
  (equicontinuousAt_iff_range.mp h‚ÇÇ).closure.continuousAt
    ‚ü®f, mem_closure_of_tendsto h‚ÇÅ <| eventually_of_forall mem_range_self‚ü©
#align filter.tendsto.continuous_at_of_equicontinuous_at Filter.Tendsto.continuousAt_of_equicontinuousAt

/-- A version of `Equicontinuous.closure` applicable to subsets of types which embed continuously
into `X ‚Üí Œ±` with the product topology. It turns out we don't need any other condition on the
embedding than continuity, but in practice this will mostly be applied to `FunLike` types where
the coercion is injective. -/
theorem Equicontinuous.closure' {A : Set Y} {u : Y ‚Üí X ‚Üí Œ±}
    (hA : Equicontinuous (u ‚àò (‚Üë) : A ‚Üí X ‚Üí Œ±)) (hu : Continuous u) :
    Equicontinuous (u ‚àò (‚Üë) : closure A ‚Üí X ‚Üí Œ±) := fun x => (hA x).closure' hu
#align equicontinuous.closure' Equicontinuous.closure'

/-- If a set of functions is equicontinuous, its closure for the product topology is also
equicontinuous. -/
theorem Equicontinuous.closure {A : Set <| X ‚Üí Œ±} (hA : A.Equicontinuous) :
    (closure A).Equicontinuous := fun x => (hA x).closure
#align equicontinuous.closure Equicontinuous.closure

/-- If `ùìï : Œπ ‚Üí X ‚Üí Œ±` tends to `f : X ‚Üí Œ±` *pointwise* along some nontrivial filter, and if the
family `ùìï` is equicontinuous, then the limit is continuous. -/
theorem Filter.Tendsto.continuous_of_equicontinuousAt {l : Filter Œπ} [l.NeBot] {F : Œπ ‚Üí X ‚Üí Œ±}
    {f : X ‚Üí Œ±} (h‚ÇÅ : Tendsto F l (ùìù f)) (h‚ÇÇ : Equicontinuous F) : Continuous f :=
  continuous_iff_continuousAt.mpr fun x => h‚ÇÅ.continuousAt_of_equicontinuousAt (h‚ÇÇ x)
#align filter.tendsto.continuous_of_equicontinuous_at Filter.Tendsto.continuous_of_equicontinuousAt

/-- A version of `UniformEquicontinuous.closure` applicable to subsets of types which embed
continuously into `Œ≤ ‚Üí Œ±` with the product topology. It turns out we don't need any other condition
on the embedding than continuity, but in practice this will mostly be applied to `FunLike` types
where the coercion is injective. -/
theorem UniformEquicontinuous.closure' {A : Set Y} {u : Y ‚Üí Œ≤ ‚Üí Œ±}
    (hA : UniformEquicontinuous (u ‚àò (‚Üë) : A ‚Üí Œ≤ ‚Üí Œ±)) (hu : Continuous u) :
    UniformEquicontinuous (u ‚àò (‚Üë) : closure A ‚Üí Œ≤ ‚Üí Œ±) := by
  intro U hU
  rcases mem_uniformity_isClosed hU with ‚ü®V, hV, hVclosed, hVU‚ü©
  filter_upwards [hA V hV]
  rintro ‚ü®x, y‚ü© hxy
  rw [SetCoe.forall] at *
  change A ‚äÜ (fun f => (u f x, u f y)) ‚Åª¬π' V at hxy
  refine' (closure_minimal hxy <| hVclosed.preimage <| _).trans (preimage_mono hVU)
  exact Continuous.prod_mk ((continuous_apply x).comp hu) ((continuous_apply y).comp hu)
#align uniform_equicontinuous.closure' UniformEquicontinuous.closure'

/-- If a set of functions is uniformly equicontinuous, its closure for the product topology is also
uniformly equicontinuous. -/
theorem UniformEquicontinuous.closure {A : Set <| Œ≤ ‚Üí Œ±} (hA : A.UniformEquicontinuous) :
    (closure A).UniformEquicontinuous :=
  UniformEquicontinuous.closure' (u := id) hA continuous_id
#align uniform_equicontinuous.closure UniformEquicontinuous.closure

/-- If `ùìï : Œπ ‚Üí Œ≤ ‚Üí Œ±` tends to `f : Œ≤ ‚Üí Œ±` *pointwise* along some nontrivial filter, and if the
family `ùìï` is uniformly equicontinuous, then the limit is uniformly continuous. -/
theorem Filter.Tendsto.uniformContinuous_of_uniformEquicontinuous {l : Filter Œπ} [l.NeBot]
    {F : Œπ ‚Üí Œ≤ ‚Üí Œ±} {f : Œ≤ ‚Üí Œ±} (h‚ÇÅ : Tendsto F l (ùìù f)) (h‚ÇÇ : UniformEquicontinuous F) :
    UniformContinuous f :=
  (uniformEquicontinuous_at_iff_range.mp h‚ÇÇ).closure.uniformContinuous
    ‚ü®f, mem_closure_of_tendsto h‚ÇÅ <| eventually_of_forall mem_range_self‚ü©
#align filter.tendsto.uniform_continuous_of_uniform_equicontinuous Filter.Tendsto.uniformContinuous_of_uniformEquicontinuous

/-- If `F : Œπ ‚Üí X ‚Üí Œ±` is a family of functions equicontinuous at `x`,
it tends to `f y` along a filter `l` for any `y ‚àà s`,
the limit function `f` tends to `z` along `ùìù[s] x`, and `x ‚àà closure s`,
then `(F ¬∑ x)` tends to `z` along `l`.

In some sense, this is a converse of `EquicontinuousAt.closure`. -/
theorem EquicontinuousAt.tendsto_of_mem_closure {l : Filter Œπ} {F : Œπ ‚Üí X ‚Üí Œ±} {f : X ‚Üí Œ±}
    {s : Set X} {x : X} {z : Œ±} (hF : EquicontinuousAt F x) (hf : Tendsto f (ùìù[s] x) (ùìù z))
    (hs : ‚àÄ y ‚àà s, Tendsto (F ¬∑ y) l (ùìù (f y))) (hx : x ‚àà closure s) :
    Tendsto (F ¬∑ x) l (ùìù z) := by
  rw [(nhds_basis_uniformity (ùì§ Œ±).basis_sets).tendsto_right_iff] at hf ‚ä¢
  intro U hU
  rcases comp_comp_symm_mem_uniformity_sets hU with ‚ü®V, hV, hVs, hVU‚ü©
  rw [mem_closure_iff_nhdsWithin_neBot] at hx
  have : ‚àÄ·∂† y in ùìù[s] x, y ‚àà s ‚àß (‚àÄ i, (F i x, F i y) ‚àà V) ‚àß (f y, z) ‚àà V :=
    eventually_mem_nhdsWithin.and <| ((hF V hV).filter_mono nhdsWithin_le_nhds).and (hf V hV)
  rcases this.exists with ‚ü®y, hys, hFy, hfy‚ü©
  filter_upwards [hs y hys (ball_mem_nhds _ hV)] with i hi
  exact hVU ‚ü®_, ‚ü®_, hFy i, (mem_ball_symmetry hVs).2 hi‚ü©, hfy‚ü©

/-- If `F : Œπ ‚Üí X ‚Üí Œ±` is an equicontinuous family of functions,
`f : X ‚Üí Œ±` is a continuous function, and `l` is a filter on `Œπ`,
then `{x | Filter.Tendsto (F ¬∑ x) l (ùìù (f x))}` is a closed set. -/
theorem Equicontinuous.isClosed_setOf_tendsto {l : Filter Œπ} {F : Œπ ‚Üí X ‚Üí Œ±} {f : X ‚Üí Œ±}
    (hF : Equicontinuous F) (hf : Continuous f) :
    IsClosed {x | Tendsto (F ¬∑ x) l (ùìù (f x))} :=
  closure_subset_iff_isClosed.mp fun x hx ‚Ü¶
    (hF x).tendsto_of_mem_closure (hf.continuousAt.mono_left inf_le_left) (fun _ ‚Ü¶ id) hx

end

end
