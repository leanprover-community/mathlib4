/-
Copyright (c) 2020 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Yury Kudryashov
-/
import Mathlib.Topology.UniformSpace.UniformConvergence
import Mathlib.Topology.UniformSpace.Equicontinuity
import Mathlib.Topology.Separation
import Mathlib.Topology.Support

#align_import topology.uniform_space.compact from "leanprover-community/mathlib"@"735b22f8f9ff9792cf4212d7cb051c4c994bc685"

/-!
# Compact separated uniform spaces

## Main statements

* `compactSpace_uniformity`: On a compact uniform space, the topology determines the
  uniform structure, entourages are exactly the neighborhoods of the diagonal.

* `uniformSpace_of_compact_t2`: every compact T2 topological structure is induced by a uniform
  structure. This uniform structure is described in the previous item.

* **Heine-Cantor** theorem: continuous functions on compact uniform spaces with values in uniform
  spaces are automatically uniformly continuous. There are several variations, the main one is
  `CompactSpace.uniformContinuous_of_continuous`.

## Implementation notes

The construction `uniformSpace_of_compact_t2` is not declared as an instance, as it would badly
loop.

## Tags

uniform space, uniform continuity, compact space
-/


open scoped Classical
open Uniformity Topology Filter UniformSpace Set

variable {Î± Î² Î³ : Type*} [UniformSpace Î±] [UniformSpace Î²]

/-!
### Uniformity on compact spaces
-/


/-- On a compact uniform space, the topology determines the uniform structure, entourages are
exactly the neighborhoods of the diagonal. -/
theorem nhdsSet_diagonal_eq_uniformity [CompactSpace Î±] : ğ“Ë¢ (diagonal Î±) = ğ“¤ Î± := by
  refine nhdsSet_diagonal_le_uniformity.antisymm ?_
  have :
    (ğ“¤ (Î± Ã— Î±)).HasBasis (fun U => U âˆˆ ğ“¤ Î±) fun U =>
      (fun p : (Î± Ã— Î±) Ã— Î± Ã— Î± => ((p.1.1, p.2.1), p.1.2, p.2.2)) â»Â¹' U Ã—Ë¢ U := by
    rw [uniformity_prod_eq_comap_prod]
    exact (ğ“¤ Î±).basis_sets.prod_self.comap _
  refine (isCompact_diagonal.nhdsSet_basis_uniformity this).ge_iff.2 fun U hU => ?_
  exact mem_of_superset hU fun âŸ¨x, yâŸ© hxy => mem_iUnionâ‚‚.2
    âŸ¨(x, x), rfl, refl_mem_uniformity hU, hxyâŸ©
#align nhds_set_diagonal_eq_uniformity nhdsSet_diagonal_eq_uniformity

/-- On a compact uniform space, the topology determines the uniform structure, entourages are
exactly the neighborhoods of the diagonal. -/
theorem compactSpace_uniformity [CompactSpace Î±] : ğ“¤ Î± = â¨† x, ğ“ (x, x) :=
  nhdsSet_diagonal_eq_uniformity.symm.trans (nhdsSet_diagonal _)
#align compact_space_uniformity compactSpace_uniformity

theorem unique_uniformity_of_compact [t : TopologicalSpace Î³] [CompactSpace Î³]
    {u u' : UniformSpace Î³} (h : u.toTopologicalSpace = t) (h' : u'.toTopologicalSpace = t) :
    u = u' := by
  refine UniformSpace.ext ?_
  have : @CompactSpace Î³ u.toTopologicalSpace := by rwa [h]
  have : @CompactSpace Î³ u'.toTopologicalSpace := by rwa [h']
  rw [@compactSpace_uniformity _ u, compactSpace_uniformity, h, h']
#align unique_uniformity_of_compact unique_uniformity_of_compact

/-- The unique uniform structure inducing a given compact topological structure. -/
def uniformSpaceOfCompactT2 [TopologicalSpace Î³] [CompactSpace Î³] [T2Space Î³] : UniformSpace Î³ where
  uniformity := ğ“Ë¢ (diagonal Î³)
  symm := continuous_swap.tendsto_nhdsSet fun x => Eq.symm
  comp := by
    /-  This is the difficult part of the proof. We need to prove that, for each neighborhood `W`
        of the diagonal `Î”`, there exists a smaller neighborhood `V` such that `V â—‹ V âŠ† W`.
        -/
    set ğ“Î” := ğ“Ë¢ (diagonal Î³)
    -- The filter of neighborhoods of Î”
    set F := ğ“Î”.lift' fun s : Set (Î³ Ã— Î³) => s â—‹ s
    -- Compositions of neighborhoods of Î”
    -- If this weren't true, then there would be V âˆˆ ğ“Î” such that F âŠ“ ğ“Ÿ Vá¶œ â‰  âŠ¥
    rw [le_iff_forall_inf_principal_compl]
    intro V V_in
    by_contra H
    haveI : NeBot (F âŠ“ ğ“Ÿ Vá¶œ) := âŸ¨HâŸ©
    -- Hence compactness would give us a cluster point (x, y) for F âŠ“ ğ“Ÿ Vá¶œ
    obtain âŸ¨âŸ¨x, yâŸ©, hxyâŸ© : âˆƒ p : Î³ Ã— Î³, ClusterPt p (F âŠ“ ğ“Ÿ Vá¶œ) := exists_clusterPt_of_compactSpace _
    -- In particular (x, y) is a cluster point of ğ“Ÿ Vá¶œ, hence is not in the interior of V,
    -- and a fortiori not in Î”, so x â‰  y
    have clV : ClusterPt (x, y) (ğ“Ÿ <| Vá¶œ) := hxy.of_inf_right
    have : (x, y) âˆ‰ interior V := by
      have : (x, y) âˆˆ closure Vá¶œ := by rwa [mem_closure_iff_clusterPt]
      rwa [closure_compl] at this
    have diag_subset : diagonal Î³ âŠ† interior V := subset_interior_iff_mem_nhdsSet.2 V_in
    have x_ne_y : x â‰  y := mt (@diag_subset (x, y)) this
    -- Since Î³ is compact and Hausdorff, it is Tâ‚„, hence Tâ‚ƒ.
    -- So there are closed neighborhoods Vâ‚ and Vâ‚‚ of x and y contained in
    -- disjoint open neighborhoods Uâ‚ and Uâ‚‚.
    obtain
      âŸ¨Uâ‚, _, Vâ‚, Vâ‚_in, Uâ‚‚, _, Vâ‚‚, Vâ‚‚_in, Vâ‚_cl, Vâ‚‚_cl, Uâ‚_op, Uâ‚‚_op, VUâ‚, VUâ‚‚, hUâ‚â‚‚âŸ© :=
      disjoint_nested_nhds x_ne_y
    -- We set Uâ‚ƒ := (Vâ‚ âˆª Vâ‚‚)á¶œ so that W := Uâ‚ Ã—Ë¢ Uâ‚ âˆª Uâ‚‚ Ã—Ë¢ Uâ‚‚ âˆª Uâ‚ƒ Ã—Ë¢ Uâ‚ƒ is an open
    -- neighborhood of Î”.
    let Uâ‚ƒ := (Vâ‚ âˆª Vâ‚‚)á¶œ
    have Uâ‚ƒ_op : IsOpen Uâ‚ƒ := (Vâ‚_cl.union Vâ‚‚_cl).isOpen_compl
    let W := Uâ‚ Ã—Ë¢ Uâ‚ âˆª Uâ‚‚ Ã—Ë¢ Uâ‚‚ âˆª Uâ‚ƒ Ã—Ë¢ Uâ‚ƒ
    have W_in : W âˆˆ ğ“Î” := by
      rw [mem_nhdsSet_iff_forall]
      rintro âŸ¨z, z'âŸ© (rfl : z = z')
      refine IsOpen.mem_nhds ?_ ?_
      Â· apply_rules [IsOpen.union, IsOpen.prod]
      Â· simp only [W, mem_union, mem_prod, and_self_iff]
        exact (_root_.em _).imp_left fun h => union_subset_union VUâ‚ VUâ‚‚ h
    -- So W â—‹ W âˆˆ F by definition of F
    have : W â—‹ W âˆˆ F := @mem_lift' _ _ _ (fun s => s â—‹ s) _ W_in
      -- Porting note: was `by simpa only using mem_lift' W_in`
    -- And Vâ‚ Ã—Ë¢ Vâ‚‚ âˆˆ ğ“ (x, y)
    have hVâ‚â‚‚ : Vâ‚ Ã—Ë¢ Vâ‚‚ âˆˆ ğ“ (x, y) := prod_mem_nhds Vâ‚_in Vâ‚‚_in
    -- But (x, y) is also a cluster point of F so (Vâ‚ Ã—Ë¢ Vâ‚‚) âˆ© (W â—‹ W) â‰  âˆ…
    -- However the construction of W implies (Vâ‚ Ã—Ë¢ Vâ‚‚) âˆ© (W â—‹ W) = âˆ….
    -- Indeed assume for contradiction there is some (u, v) in the intersection.
    obtain âŸ¨âŸ¨u, vâŸ©, âŸ¨u_in, v_inâŸ©, w, huw, hwvâŸ© := clusterPt_iff.mp hxy.of_inf_left hVâ‚â‚‚ this
    -- So u âˆˆ Vâ‚, v âˆˆ Vâ‚‚, and there exists some w such that (u, w) âˆˆ W and (w ,v) âˆˆ W.
    -- Because u is in Vâ‚ which is disjoint from Uâ‚‚ and Uâ‚ƒ, (u, w) âˆˆ W forces (u, w) âˆˆ Uâ‚ Ã—Ë¢ Uâ‚.
    have uw_in : (u, w) âˆˆ Uâ‚ Ã—Ë¢ Uâ‚ :=
      (huw.resolve_right fun h => h.1 <| Or.inl u_in).resolve_right fun h =>
        hUâ‚â‚‚.le_bot âŸ¨VUâ‚ u_in, h.1âŸ©
    -- Similarly, because v âˆˆ Vâ‚‚, (w ,v) âˆˆ W forces (w, v) âˆˆ Uâ‚‚ Ã—Ë¢ Uâ‚‚.
    have wv_in : (w, v) âˆˆ Uâ‚‚ Ã—Ë¢ Uâ‚‚ :=
      (hwv.resolve_right fun h => h.2 <| Or.inr v_in).resolve_left fun h =>
        hUâ‚â‚‚.le_bot âŸ¨h.2, VUâ‚‚ v_inâŸ©
    -- Hence w âˆˆ Uâ‚ âˆ© Uâ‚‚ which is empty.
    -- So we have a contradiction
    exact hUâ‚â‚‚.le_bot âŸ¨uw_in.2, wv_in.1âŸ©
  nhds_eq_comap_uniformity x := by
    simp_rw [nhdsSet_diagonal, comap_iSup, nhds_prod_eq, comap_prod, (Â· âˆ˜ Â·), comap_id']
    rw [iSup_split_single _ x, comap_const_of_mem fun V => mem_of_mem_nhds]
    suffices âˆ€ y â‰  x, comap (fun _ : Î³ â†¦ x) (ğ“ y) âŠ“ ğ“ y â‰¤ ğ“ x by simpa
    intro y hxy
    simp [comap_const_of_not_mem (compl_singleton_mem_nhds hxy) (not_not_intro rfl)]
#align uniform_space_of_compact_t2 uniformSpaceOfCompactT2

/-!
###Â Heine-Cantor theorem
-/


/-- Heine-Cantor: a continuous function on a compact uniform space is uniformly
continuous. -/
theorem CompactSpace.uniformContinuous_of_continuous [CompactSpace Î±] {f : Î± â†’ Î²}
    (h : Continuous f) : UniformContinuous f :=
calc map (Prod.map f f) (ğ“¤ Î±)
   = map (Prod.map f f) (ğ“Ë¢ (diagonal Î±)) := by rw [nhdsSet_diagonal_eq_uniformity]
 _ â‰¤ ğ“Ë¢ (diagonal Î²)                      := (h.prod_map h).tendsto_nhdsSet mapsTo_prod_map_diagonal
 _ â‰¤ ğ“¤ Î²                                  := nhdsSet_diagonal_le_uniformity
#align compact_space.uniform_continuous_of_continuous CompactSpace.uniformContinuous_of_continuous

/-- Heine-Cantor: a continuous function on a compact set of a uniform space is uniformly
continuous. -/
theorem IsCompact.uniformContinuousOn_of_continuous {s : Set Î±} {f : Î± â†’ Î²} (hs : IsCompact s)
    (hf : ContinuousOn f s) : UniformContinuousOn f s := by
  rw [uniformContinuousOn_iff_restrict]
  rw [isCompact_iff_compactSpace] at hs
  rw [continuousOn_iff_continuous_restrict] at hf
  exact CompactSpace.uniformContinuous_of_continuous hf
#align is_compact.uniform_continuous_on_of_continuous IsCompact.uniformContinuousOn_of_continuous

/-- If `s` is compact and `f` is continuous at all points of `s`, then `f` is
"uniformly continuous at the set `s`", i.e. `f x` is close to `f y` whenever `x âˆˆ s` and `y` is
close to `x` (even if `y` is not itself in `s`, so this is a stronger assertion than
`UniformContinuousOn s`). -/
theorem IsCompact.uniformContinuousAt_of_continuousAt {r : Set (Î² Ã— Î²)} {s : Set Î±}
    (hs : IsCompact s) (f : Î± â†’ Î²) (hf : âˆ€ a âˆˆ s, ContinuousAt f a) (hr : r âˆˆ ğ“¤ Î²) :
    { x : Î± Ã— Î± | x.1 âˆˆ s â†’ (f x.1, f x.2) âˆˆ r } âˆˆ ğ“¤ Î± := by
  obtain âŸ¨t, ht, htsymm, htrâŸ© := comp_symm_mem_uniformity_sets hr
  choose U hU T hT hb using fun a ha =>
    exists_mem_nhds_ball_subset_of_mem_nhds ((hf a ha).preimage_mem_nhds <| mem_nhds_left _ ht)
  obtain âŸ¨fs, hsUâŸ© := hs.elim_nhds_subcover' U hU
  apply mem_of_superset ((biInter_finset_mem fs).2 fun a _ => hT a a.2)
  rintro âŸ¨aâ‚, aâ‚‚âŸ© h hâ‚
  obtain âŸ¨a, ha, haUâŸ© := Set.mem_iUnionâ‚‚.1 (hsU hâ‚)
  apply htr
  refine âŸ¨f a, htsymm.mk_mem_comm.1 (hb _ _ _ haU ?_), hb _ _ _ haU ?_âŸ©
  exacts [mem_ball_self _ (hT a a.2), mem_iInterâ‚‚.1 h a ha]
#align is_compact.uniform_continuous_at_of_continuous_at IsCompact.uniformContinuousAt_of_continuousAt

theorem Continuous.uniformContinuous_of_tendsto_cocompact {f : Î± â†’ Î²} {x : Î²}
    (h_cont : Continuous f) (hx : Tendsto f (cocompact Î±) (ğ“ x)) : UniformContinuous f :=
  uniformContinuous_def.2 fun r hr => by
    obtain âŸ¨t, ht, htsymm, htrâŸ© := comp_symm_mem_uniformity_sets hr
    obtain âŸ¨s, hs, hstâŸ© := mem_cocompact.1 (hx <| mem_nhds_left _ ht)
    apply
      mem_of_superset
        (symmetrize_mem_uniformity <|
          (hs.uniformContinuousAt_of_continuousAt f fun _ _ => h_cont.continuousAt) <|
            symmetrize_mem_uniformity hr)
    rintro âŸ¨bâ‚, bâ‚‚âŸ© h
    by_cases hâ‚ : bâ‚ âˆˆ s; Â· exact (h.1 hâ‚).1
    by_cases hâ‚‚ : bâ‚‚ âˆˆ s; Â· exact (h.2 hâ‚‚).2
    apply htr
    exact âŸ¨x, htsymm.mk_mem_comm.1 (hst hâ‚), hst hâ‚‚âŸ©
#align continuous.uniform_continuous_of_tendsto_cocompact Continuous.uniformContinuous_of_tendsto_cocompact

/-- If `f` has compact multiplicative support, then `f` tends to 1 at infinity. -/
@[to_additive "If `f` has compact support, then `f` tends to zero at infinity."]
theorem HasCompactMulSupport.is_one_at_infty {f : Î± â†’ Î³} [TopologicalSpace Î³] [One Î³]
    (h : HasCompactMulSupport f) : Tendsto f (cocompact Î±) (ğ“ 1) := by
  -- Porting note: move to src/topology/support.lean once the port is over
  intro N hN
  rw [mem_map, mem_cocompact']
  refine âŸ¨mulTSupport f, h.isCompact, ?_âŸ©
  rw [compl_subset_comm]
  intro v hv
  rw [mem_preimage, image_eq_one_of_nmem_mulTSupport hv]
  exact mem_of_mem_nhds hN
#align has_compact_mul_support.is_one_at_infty HasCompactMulSupport.is_one_at_infty
#align has_compact_support.is_zero_at_infty HasCompactSupport.is_zero_at_infty

@[to_additive]
theorem HasCompactMulSupport.uniformContinuous_of_continuous {f : Î± â†’ Î²} [One Î²]
    (h1 : HasCompactMulSupport f) (h2 : Continuous f) : UniformContinuous f :=
  h2.uniformContinuous_of_tendsto_cocompact h1.is_one_at_infty
#align has_compact_mul_support.uniform_continuous_of_continuous HasCompactMulSupport.uniformContinuous_of_continuous
#align has_compact_support.uniform_continuous_of_continuous HasCompactSupport.uniformContinuous_of_continuous

/-- A family of functions `Î± â†’ Î² â†’ Î³` tends uniformly to its value at `x` if `Î±` is locally compact,
`Î²` is compact and `f` is continuous on `U Ã— (univ : Set Î²)` for some neighborhood `U` of `x`. -/
theorem ContinuousOn.tendstoUniformly [LocallyCompactSpace Î±] [CompactSpace Î²] [UniformSpace Î³]
    {f : Î± â†’ Î² â†’ Î³} {x : Î±} {U : Set Î±} (hxU : U âˆˆ ğ“ x) (h : ContinuousOn (â†¿f) (U Ã—Ë¢ univ)) :
    TendstoUniformly f (f x) (ğ“ x) := by
  rcases LocallyCompactSpace.local_compact_nhds _ _ hxU with âŸ¨K, hxK, hKU, hKâŸ©
  have : UniformContinuousOn (â†¿f) (K Ã—Ë¢ univ) :=
    IsCompact.uniformContinuousOn_of_continuous (hK.prod isCompact_univ)
      (h.mono <| prod_mono hKU Subset.rfl)
  exact this.tendstoUniformly hxK
#align continuous_on.tendsto_uniformly ContinuousOn.tendstoUniformly

/-- A continuous family of functions `Î± â†’ Î² â†’ Î³` tends uniformly to its value at `x`
if `Î±` is weakly locally compact and `Î²` is compact. -/
theorem Continuous.tendstoUniformly [WeaklyLocallyCompactSpace Î±] [CompactSpace Î²] [UniformSpace Î³]
    (f : Î± â†’ Î² â†’ Î³) (h : Continuous â†¿f) (x : Î±) : TendstoUniformly f (f x) (ğ“ x) :=
  let âŸ¨K, hK, hxKâŸ© := exists_compact_mem_nhds x
  have : UniformContinuousOn (â†¿f) (K Ã—Ë¢ univ) :=
    IsCompact.uniformContinuousOn_of_continuous (hK.prod isCompact_univ) h.continuousOn
  this.tendstoUniformly hxK
#align continuous.tendsto_uniformly Continuous.tendstoUniformly

/-- In a product space `Î± Ã— Î²`, assume that a function `f` is continuous on `s Ã— k` where `k` is
compact. Then, along the fiber above any `q âˆˆ s`, `f` is transversely uniformly continuous, i.e.,
if `p âˆˆ s` is close enough to `q`, then `f p x` is uniformly close to `f q x` for all `x âˆˆ k`. -/
lemma IsCompact.mem_uniformity_of_prod
    {Î± Î² E : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²] [UniformSpace E]
    {f : Î± â†’ Î² â†’ E} {s : Set Î±} {k : Set Î²} {q : Î±} {u : Set (E Ã— E)}
    (hk : IsCompact k) (hf : ContinuousOn f.uncurry (s Ã—Ë¢ k)) (hq : q âˆˆ s) (hu : u âˆˆ ğ“¤ E) :
    âˆƒ v âˆˆ ğ“[s] q, âˆ€ p âˆˆ v, âˆ€ x âˆˆ k, (f p x, f q x) âˆˆ u := by
  apply hk.induction_on (p := fun t â†¦ âˆƒ v âˆˆ ğ“[s] q, âˆ€ p âˆˆ v, âˆ€ x âˆˆ t, (f p x, f q x) âˆˆ u)
  Â· exact âŸ¨univ, univ_mem, by simpâŸ©
  Â· intro t' t ht't âŸ¨v, v_mem, hvâŸ©
    exact âŸ¨v, v_mem, fun p hp x hx â†¦ hv p hp x (ht't hx)âŸ©
  Â· intro t t' âŸ¨v, v_mem, hvâŸ© âŸ¨v', v'_mem, hv'âŸ©
    refine âŸ¨v âˆ© v', inter_mem v_mem v'_mem, fun p hp x hx â†¦ ?_âŸ©
    rcases hx with h'x|h'x
    Â· exact hv p hp.1 x h'x
    Â· exact hv' p hp.2 x h'x
  Â· rcases comp_symm_of_uniformity hu with âŸ¨u', u'_mem, u'_symm, hu'âŸ©
    intro x hx
    obtain âŸ¨v, hv, w, hw, hvwâŸ© :
      âˆƒ v âˆˆ ğ“[s] q, âˆƒ w âˆˆ ğ“[k] x, v Ã—Ë¢ w âŠ† f.uncurry â»Â¹' {z | (f q x, z) âˆˆ u'} :=
        mem_nhdsWithin_prod_iff.1 (hf (q, x) âŸ¨hq, hxâŸ© (mem_nhds_left (f q x) u'_mem))
    refine âŸ¨w, hw, v, hv, fun p hp y hy â†¦ ?_âŸ©
    have A : (f q x, f p y) âˆˆ u' := hvw (âŸ¨hp, hyâŸ© : (p, y) âˆˆ v Ã—Ë¢ w)
    have B : (f q x, f q y) âˆˆ u' := hvw (âŸ¨mem_of_mem_nhdsWithin hq hv, hyâŸ© : (q, y) âˆˆ v Ã—Ë¢ w)
    exact hu' (prod_mk_mem_compRel (u'_symm A) B)

section UniformConvergence

/-- An equicontinuous family of functions defined on a compact uniform space is automatically
uniformly equicontinuous. -/
theorem CompactSpace.uniformEquicontinuous_of_equicontinuous {Î¹ : Type*} {F : Î¹ â†’ Î² â†’ Î±}
    [CompactSpace Î²] (h : Equicontinuous F) : UniformEquicontinuous F := by
  rw [equicontinuous_iff_continuous] at h
  rw [uniformEquicontinuous_iff_uniformContinuous]
  exact CompactSpace.uniformContinuous_of_continuous h
#align compact_space.uniform_equicontinuous_of_equicontinuous CompactSpace.uniformEquicontinuous_of_equicontinuous

end UniformConvergence
