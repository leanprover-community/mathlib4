/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Patrick Massot
-/
module

public import Mathlib.Topology.UniformSpace.Basic
public import Mathlib.Topology.Compactness.Compact

/-!
# Compact sets in uniform spaces

* `compactSpace_uniformity`: On a compact uniform space, the topology determines the
  uniform structure, entourages are exactly the neighborhoods of the diagonal.

-/

public section

universe u v ua ub uc ud

variable {Î± : Type ua} {Î² : Type ub} {Î³ : Type uc} {Î´ : Type ud} {Î¹ : Sort*}

section Compact

open Uniformity Set Filter UniformSpace
open scoped SetRel Topology

variable [UniformSpace Î±] {K : Set Î±}

/-- Let `c : Î¹ â†’ Set Î±` be an open cover of a compact set `s`. Then there exists an entourage
`n` such that for each `x âˆˆ s` its `n`-neighborhood is contained in some `c i`. -/
theorem lebesgue_number_lemma {Î¹ : Sort*} {U : Î¹ â†’ Set Î±} (hK : IsCompact K)
    (hopen : âˆ€ i, IsOpen (U i)) (hcover : K âŠ† â‹ƒ i, U i) :
    âˆƒ V âˆˆ ğ“¤ Î±, âˆ€ x âˆˆ K, âˆƒ i, ball x V âŠ† U i := by
  have : âˆ€ x âˆˆ K, âˆƒ i, âˆƒ V âˆˆ ğ“¤ Î±, ball x (V â—‹ V) âŠ† U i := fun x hx â†¦ by
    obtain âŸ¨i, hiâŸ© := mem_iUnion.1 (hcover hx)
    rw [â† (hopen i).mem_nhds_iff, nhds_eq_comap_uniformity, â† lift'_comp_uniformity] at hi
    exact âŸ¨i, (((basis_sets _).lift' <| monotone_id.relComp monotone_id).comap _).mem_iff.1 hiâŸ©
  choose ind W hW hWU using this
  rcases hK.elim_nhds_subcover' (fun x hx â†¦ ball x (W x hx)) (fun x hx â†¦ ball_mem_nhds _ (hW x hx))
    with âŸ¨t, htâŸ©
  refine âŸ¨â‹‚ x âˆˆ t, W x x.2, (biInter_finset_mem _).2 fun x _ â†¦ hW x x.2, fun x hx â†¦ ?_âŸ©
  rcases mem_iUnionâ‚‚.1 (ht hx) with âŸ¨y, hyt, hxyâŸ©
  exact âŸ¨ind y y.2, fun z hz â†¦ hWU _ _ âŸ¨x, hxy, mem_iInterâ‚‚.1 hz _ hytâŸ©âŸ©

theorem lebesgue_number_lemma_nhds' {U : (x : Î±) â†’ x âˆˆ K â†’ Set Î±} (hK : IsCompact K)
    (hU : âˆ€ x hx, U x hx âˆˆ ğ“ x) : âˆƒ V âˆˆ ğ“¤ Î±, âˆ€ x âˆˆ K, âˆƒ y : K, ball x V âŠ† U y y.2 := by
  rcases lebesgue_number_lemma (U := fun x : K => interior (U x x.2)) hK (fun _ => isOpen_interior)
    (fun x hx => mem_iUnion.2 âŸ¨âŸ¨x, hxâŸ©, mem_interior_iff_mem_nhds.2 (hU x hx)âŸ©) with âŸ¨V, V_uni, hVâŸ©
  exact âŸ¨V, V_uni, fun x hx => (hV x hx).imp fun _ hy => hy.trans interior_subsetâŸ©

theorem lebesgue_number_lemma_nhds {U : Î± â†’ Set Î±} (hK : IsCompact K) (hU : âˆ€ x âˆˆ K, U x âˆˆ ğ“ x) :
    âˆƒ V âˆˆ ğ“¤ Î±, âˆ€ x âˆˆ K, âˆƒ y, ball x V âŠ† U y := by
  rcases lebesgue_number_lemma (U := fun x => interior (U x)) hK (fun _ => isOpen_interior)
    (fun x hx => mem_iUnion.2 âŸ¨x, mem_interior_iff_mem_nhds.2 (hU x hx)âŸ©) with âŸ¨V, V_uni, hVâŸ©
  exact âŸ¨V, V_uni, fun x hx => (hV x hx).imp fun _ hy => hy.trans interior_subsetâŸ©

theorem lebesgue_number_lemma_nhdsWithin' {U : (x : Î±) â†’ x âˆˆ K â†’ Set Î±} (hK : IsCompact K)
    (hU : âˆ€ x hx, U x hx âˆˆ ğ“[K] x) : âˆƒ V âˆˆ ğ“¤ Î±, âˆ€ x âˆˆ K, âˆƒ y : K, ball x V âˆ© K âŠ† U y y.2 :=
  (lebesgue_number_lemma_nhds' hK (fun x hx => Filter.mem_inf_principal'.1 (hU x hx))).imp
    fun _ âŸ¨V_uni, hVâŸ© => âŸ¨V_uni, fun x hx => (hV x hx).imp fun _ hy => (inter_subset _ _ _).2 hyâŸ©

theorem lebesgue_number_lemma_nhdsWithin {U : Î± â†’ Set Î±} (hK : IsCompact K)
    (hU : âˆ€ x âˆˆ K, U x âˆˆ ğ“[K] x) : âˆƒ V âˆˆ ğ“¤ Î±, âˆ€ x âˆˆ K, âˆƒ y, ball x V âˆ© K âŠ† U y :=
  (lebesgue_number_lemma_nhds hK (fun x hx => Filter.mem_inf_principal'.1 (hU x hx))).imp
    fun _ âŸ¨V_uni, hVâŸ© => âŸ¨V_uni, fun x hx => (hV x hx).imp fun _ hy => (inter_subset _ _ _).2 hyâŸ©

/-- Let `U : Î¹ â†’ Set Î±` be an open cover of a compact set `K`.
Then there exists an entourage `V`
such that for each `x âˆˆ K` its `V`-neighborhood is included in some `U i`.

Moreover, one can choose an entourage from a given basis. -/
protected theorem Filter.HasBasis.lebesgue_number_lemma {Î¹' Î¹ : Sort*} {p : Î¹' â†’ Prop}
    {V : Î¹' â†’ Set (Î± Ã— Î±)} {U : Î¹ â†’ Set Î±} (hbasis : (ğ“¤ Î±).HasBasis p V) (hK : IsCompact K)
    (hopen : âˆ€ j, IsOpen (U j)) (hcover : K âŠ† â‹ƒ j, U j) :
    âˆƒ i, p i âˆ§ âˆ€ x âˆˆ K, âˆƒ j, ball x (V i) âŠ† U j := by
  refine (hbasis.exists_iff ?_).1 (lebesgue_number_lemma hK hopen hcover)
  exact fun s t hst ht x hx â†¦ (ht x hx).imp fun i hi â†¦ Subset.trans (ball_mono hst _) hi

protected theorem Filter.HasBasis.lebesgue_number_lemma_nhds' {Î¹' : Sort*} {p : Î¹' â†’ Prop}
    {V : Î¹' â†’ Set (Î± Ã— Î±)} {U : (x : Î±) â†’ x âˆˆ K â†’ Set Î±} (hbasis : (ğ“¤ Î±).HasBasis p V)
    (hK : IsCompact K) (hU : âˆ€ x hx, U x hx âˆˆ ğ“ x) :
    âˆƒ i, p i âˆ§ âˆ€ x âˆˆ K, âˆƒ y : K, ball x (V i) âŠ† U y y.2 := by
  refine (hbasis.exists_iff ?_).1 (lebesgue_number_lemma_nhds' hK hU)
  exact fun s t hst ht x hx â†¦ (ht x hx).imp fun y hy â†¦ Subset.trans (ball_mono hst _) hy

protected theorem Filter.HasBasis.lebesgue_number_lemma_nhds {Î¹' : Sort*} {p : Î¹' â†’ Prop}
    {V : Î¹' â†’ Set (Î± Ã— Î±)} {U : Î± â†’ Set Î±} (hbasis : (ğ“¤ Î±).HasBasis p V) (hK : IsCompact K)
    (hU : âˆ€ x âˆˆ K, U x âˆˆ ğ“ x) : âˆƒ i, p i âˆ§ âˆ€ x âˆˆ K, âˆƒ y, ball x (V i) âŠ† U y := by
  refine (hbasis.exists_iff ?_).1 (lebesgue_number_lemma_nhds hK hU)
  exact fun s t hst ht x hx â†¦ (ht x hx).imp fun y hy â†¦ Subset.trans (ball_mono hst _) hy

protected theorem Filter.HasBasis.lebesgue_number_lemma_nhdsWithin' {Î¹' : Sort*} {p : Î¹' â†’ Prop}
    {V : Î¹' â†’ Set (Î± Ã— Î±)} {U : (x : Î±) â†’ x âˆˆ K â†’ Set Î±} (hbasis : (ğ“¤ Î±).HasBasis p V)
    (hK : IsCompact K) (hU : âˆ€ x hx, U x hx âˆˆ ğ“[K] x) :
    âˆƒ i, p i âˆ§ âˆ€ x âˆˆ K, âˆƒ y : K, ball x (V i) âˆ© K âŠ† U y y.2 := by
  refine (hbasis.exists_iff ?_).1 (lebesgue_number_lemma_nhdsWithin' hK hU)
  exact fun s t hst ht x hx â†¦ (ht x hx).imp
    fun y hy â†¦ Subset.trans (Set.inter_subset_inter_left K (ball_mono hst _)) hy

protected theorem Filter.HasBasis.lebesgue_number_lemma_nhdsWithin {Î¹' : Sort*} {p : Î¹' â†’ Prop}
    {V : Î¹' â†’ Set (Î± Ã— Î±)} {U : Î± â†’ Set Î±} (hbasis : (ğ“¤ Î±).HasBasis p V) (hK : IsCompact K)
    (hU : âˆ€ x âˆˆ K, U x âˆˆ ğ“[K] x) : âˆƒ i, p i âˆ§ âˆ€ x âˆˆ K, âˆƒ y, ball x (V i) âˆ© K âŠ† U y := by
  refine (hbasis.exists_iff ?_).1 (lebesgue_number_lemma_nhdsWithin hK hU)
  exact fun s t hst ht x hx â†¦ (ht x hx).imp
    fun y hy â†¦ Subset.trans (Set.inter_subset_inter_left K (ball_mono hst _)) hy

/-- Let `c : Set (Set Î±)` be an open cover of a compact set `s`. Then there exists an entourage
`n` such that for each `x âˆˆ s` its `n`-neighborhood is contained in some `t âˆˆ c`. -/
theorem lebesgue_number_lemma_sUnion {S : Set (Set Î±)}
    (hK : IsCompact K) (hopen : âˆ€ s âˆˆ S, IsOpen s) (hcover : K âŠ† â‹ƒâ‚€ S) :
    âˆƒ V âˆˆ ğ“¤ Î±, âˆ€ x âˆˆ K, âˆƒ s âˆˆ S, ball x V âŠ† s := by
  rw [sUnion_eq_iUnion] at hcover
  simpa using lebesgue_number_lemma hK (by simpa) hcover

/-- If `K` is a compact set in a uniform space and `{V i | p i}` is a basis of entourages,
then `{â‹ƒ x âˆˆ K, UniformSpace.ball x (V i) | p i}` is a basis of `ğ“Ë¢ K`.

Here "`{s i | p i}` is a basis of a filter `l`" means `Filter.HasBasis l p s`. -/
theorem IsCompact.nhdsSet_basis_uniformity {p : Î¹ â†’ Prop} {V : Î¹ â†’ Set (Î± Ã— Î±)}
    (hbasis : (ğ“¤ Î±).HasBasis p V) (hK : IsCompact K) :
    (ğ“Ë¢ K).HasBasis p fun i => â‹ƒ x âˆˆ K, ball x (V i) where
  mem_iff' U := by
    constructor
    Â· intro H
      have HKU : K âŠ† â‹ƒ _ : Unit, interior U := by
        simpa only [iUnion_const, subset_interior_iff_mem_nhdsSet] using H
      obtain âŸ¨i, hpi, hiâŸ© : âˆƒ i, p i âˆ§ â‹ƒ x âˆˆ K, ball x (V i) âŠ† interior U := by
        simpa using hbasis.lebesgue_number_lemma hK (fun _ â†¦ isOpen_interior) HKU
      exact âŸ¨i, hpi, hi.trans interior_subsetâŸ©
    Â· rintro âŸ¨i, hpi, hiâŸ©
      refine mem_of_superset (bUnion_mem_nhdsSet fun x _ â†¦ ?_) hi
      exact ball_mem_nhds _ <| hbasis.mem_of_mem hpi

-- TODO: move to a separate file, golf using the regularity of a uniform space.
theorem Disjoint.exists_uniform_thickening {A B : Set Î±} (hA : IsCompact A) (hB : IsClosed B)
    (h : Disjoint A B) : âˆƒ V âˆˆ ğ“¤ Î±, Disjoint (â‹ƒ x âˆˆ A, ball x V) (â‹ƒ x âˆˆ B, ball x V) := by
  have : Bá¶œ âˆˆ ğ“Ë¢ A := hB.isOpen_compl.mem_nhdsSet.mpr h.le_compl_right
  rw [(hA.nhdsSet_basis_uniformity (Filter.basis_sets _)).mem_iff] at this
  rcases this with âŸ¨U, hU, hUABâŸ©
  rcases comp_symm_mem_uniformity_sets hU with âŸ¨V, hV, hVsymm, hVUâŸ©
  refine âŸ¨V, hV, Set.disjoint_left.mpr fun x => ?_âŸ©
  simp only [mem_iUnionâ‚‚]
  rintro âŸ¨a, ha, hxaâŸ© âŸ¨b, hb, hxbâŸ©
  rw [mem_ball_symmetry] at hxa hxb
  exact hUAB (mem_iUnionâ‚‚_of_mem ha <| hVU <| mem_comp_of_mem_ball hxa hxb) hb

theorem Disjoint.exists_uniform_thickening_of_basis {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set (Î± Ã— Î±)}
    (hU : (ğ“¤ Î±).HasBasis p s) {A B : Set Î±} (hA : IsCompact A) (hB : IsClosed B)
    (h : Disjoint A B) : âˆƒ i, p i âˆ§ Disjoint (â‹ƒ x âˆˆ A, ball x (s i)) (â‹ƒ x âˆˆ B, ball x (s i)) := by
  rcases h.exists_uniform_thickening hA hB with âŸ¨V, hV, hVABâŸ©
  rcases hU.mem_iff.1 hV with âŸ¨i, hi, hiVâŸ©
  exact âŸ¨i, hi, hVAB.mono (iUnionâ‚‚_mono fun a _ => ball_mono hiV a)
    (iUnionâ‚‚_mono fun b _ => ball_mono hiV b)âŸ©

/-- A useful consequence of the Lebesgue number lemma: given any compact set `K` contained in an
open set `U`, we can find an (open) entourage `V` such that the ball of size `V` about any point of
`K` is contained in `U`. -/
theorem lebesgue_number_of_compact_open {K U : Set Î±} (hK : IsCompact K)
    (hU : IsOpen U) (hKU : K âŠ† U) : âˆƒ V âˆˆ ğ“¤ Î±, IsOpen V âˆ§ âˆ€ x âˆˆ K, UniformSpace.ball x V âŠ† U :=
  let âŸ¨V, âŸ¨hV, hVoâŸ©, hVUâŸ© :=
    (hK.nhdsSet_basis_uniformity uniformity_hasBasis_open).mem_iff.1 (hU.mem_nhdsSet.2 hKU)
  âŸ¨V, hV, hVo, iUnionâ‚‚_subset_iff.1 hVUâŸ©


/-- On a compact uniform space, the topology determines the uniform structure, entourages are
exactly the neighborhoods of the diagonal. -/
theorem nhdsSet_diagonal_eq_uniformity [CompactSpace Î±] : ğ“Ë¢ (diagonal Î±) = ğ“¤ Î± := by
  refine nhdsSet_diagonal_le_uniformity.antisymm ?_
  have :
    (ğ“¤ (Î± Ã— Î±)).HasBasis (fun U => U âˆˆ ğ“¤ Î±) fun U =>
      (fun p : (Î± Ã— Î±) Ã— Î± Ã— Î± => ((p.1.1, p.2.1), p.1.2, p.2.2)) â»Â¹' U Ã—Ë¢ U := by
    rw [uniformity_prod_eq_comap_prod]
    exact (ğ“¤ Î±).basis_sets.prod_self.comap _
  refine (isCompact_diagonal.nhdsSet_basis_uniformity this).ge_iff.2 fun U hU => ?_
  exact mem_of_superset hU fun âŸ¨x, yâŸ© hxy => mem_iUnionâ‚‚.2
    âŸ¨(x, x), rfl, refl_mem_uniformity hU, hxyâŸ©

/-- On a compact uniform space, the topology determines the uniform structure, entourages are
exactly the neighborhoods of the diagonal. -/
theorem compactSpace_uniformity [CompactSpace Î±] : ğ“¤ Î± = â¨† x, ğ“ (x, x) :=
  nhdsSet_diagonal_eq_uniformity.symm.trans (nhdsSet_diagonal _)

theorem unique_uniformity_of_compact [t : TopologicalSpace Î³] [CompactSpace Î³]
    {u u' : UniformSpace Î³} (h : u.toTopologicalSpace = t) (h' : u'.toTopologicalSpace = t) :
    u = u' := by
  refine UniformSpace.ext ?_
  have : @CompactSpace Î³ u.toTopologicalSpace := by rwa [h]
  have : @CompactSpace Î³ u'.toTopologicalSpace := by rwa [h']
  rw [@compactSpace_uniformity _ u, compactSpace_uniformity, h, h']

end Compact

theorem IsClosed.relPreimage_of_isCompact [TopologicalSpace Î±] [TopologicalSpace Î²]
    {s : SetRel Î± Î²} (hs : IsClosed s) {t : Set Î²} (ht : IsCompact t) :
    IsClosed (s.preimage t) := by
  rw [â† isOpen_compl_iff, isOpen_iff_eventually] at hs âŠ¢
  simp_rw [Set.mem_compl_iff, SetRel.mem_preimage, not_exists, not_and]
  exact fun y hy => ht.eventually_forall_of_forall_eventually fun x hx => hs _ <| hy _ hx

theorem IsClosed.relImage_of_isCompact [TopologicalSpace Î±] [TopologicalSpace Î²]
    {s : SetRel Î± Î²} (hs : IsClosed s) {t : Set Î±} (ht : IsCompact t) :
    IsClosed (s.image t) :=
  hs.relInv.relPreimage_of_isCompact ht
