/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Patrick Massot
-/
import Mathlib.Data.Rel
import Mathlib.Order.Filter.SmallSets
import Mathlib.Topology.UniformSpace.Defs
import Mathlib.Topology.ContinuousOn

/-!
# Basic results on uniform spaces

Uniform spaces are a generalization of metric spaces and topological groups.

## Main definitions

In this file we define a complete lattice structure on the type `UniformSpace X`
of uniform structures on `X`, as well as the pullback (`UniformSpace.comap`) of uniform structures
coming from the pullback of filters.
Like distance functions, uniform structures cannot be pushed forward in general.

## Notation

Localized in `Uniformity`, we have the notation `ğ“¤ X` for the uniformity on a uniform space `X`,
and `â—‹` for composition of relations, seen as terms with type `Set (X Ã— X)`.

## References

The formalization uses the books:

* [N. Bourbaki, *General Topology*][bourbaki1966]
* [I. M. James, *Topologies and Uniformities*][james1999]

But it makes a more systematic use of the filter library.
-/

open Set Filter Topology
open scoped SetRel Uniformity

universe u v ua ub uc ud

/-!
### Relations, seen as `SetRel Î± Î±`
-/

variable {Î± : Type ua} {Î² : Type ub} {Î³ : Type uc} {Î´ : Type ud} {Î¹ : Sort*}

open scoped SetRel in
lemma IsOpen.relComp [TopologicalSpace Î±] [TopologicalSpace Î²] [TopologicalSpace Î³]
    {s : SetRel Î± Î²} {t : SetRel Î² Î³} (hs : IsOpen s) (ht : IsOpen t) : IsOpen (s â—‹ t) := by
  conv =>
    arg 1; equals â‹ƒ b, (fun p => (p.1, b)) â»Â¹' s âˆ© (fun p => (b, p.2)) â»Â¹' t => ext âŸ¨_, _âŸ©; simp
  exact isOpen_iUnion fun a â†¦ hs.preimage (by fun_prop) |>.inter <| ht.preimage (by fun_prop)

lemma IsOpen.relInv [TopologicalSpace Î±] [TopologicalSpace Î²]
    {s : SetRel Î± Î²} (hs : IsOpen s) : IsOpen s.inv :=
  hs.preimage continuous_swap

lemma IsOpen.relImage [TopologicalSpace Î±] [TopologicalSpace Î²]
    {s : SetRel Î± Î²} (hs : IsOpen s) {t : Set Î±} : IsOpen (s.image t) := by
  simp_rw [SetRel.image, â† exists_prop, Set.setOf_exists]
  exact isOpen_biUnion fun _ _ => hs.preimage <| .prodMk_right _

lemma IsOpen.relPreimage [TopologicalSpace Î±] [TopologicalSpace Î²]
    {s : SetRel Î± Î²} (hs : IsOpen s) {t : Set Î²} : IsOpen (s.preimage t) :=
  hs.relInv.relImage

section UniformSpace

variable [UniformSpace Î±]

/-- If `s âˆˆ ğ“¤ Î±`, then for any natural `n`, for a subset `t` of a sufficiently small set in `ğ“¤ Î±`,
we have `t â—‹ t â—‹ ... â—‹ t âŠ† s` (`n` compositions). -/
theorem eventually_uniformity_iterate_comp_subset {s : SetRel Î± Î±} (hs : s âˆˆ ğ“¤ Î±) (n : â„•) :
    âˆ€á¶  t in (ğ“¤ Î±).smallSets, (t â—‹ Â·)^[n] t âŠ† s := by
  suffices âˆ€á¶  t in (ğ“¤ Î±).smallSets, t âŠ† s âˆ§ (t â—‹ Â·)^[n] t âŠ† s from (eventually_and.1 this).2
  induction n generalizing s with
  | zero => simpa
  | succ _ ihn =>
    rcases comp_mem_uniformity_sets hs with âŸ¨t, htU, htsâŸ©
    refine (ihn htU).mono fun U hU => ?_
    rw [Function.iterate_succ_apply']
    have : SetRel.IsRefl t := SetRel.id_subset_iff.1 <| refl_le_uniformity htU
    exact âŸ¨hU.1.trans <| SetRel.left_subset_comp.trans hts,
     (SetRel.comp_subset_comp hU.1 hU.2).trans htsâŸ©

/-- If `s âˆˆ ğ“¤ Î±`, then for a subset `t` of a sufficiently small set in `ğ“¤ Î±`,
we have `t â—‹ t âŠ† s`. -/
theorem eventually_uniformity_comp_subset {s : SetRel Î± Î±} (hs : s âˆˆ ğ“¤ Î±) :
    âˆ€á¶  t in (ğ“¤ Î±).smallSets, t â—‹ t âŠ† s :=
  eventually_uniformity_iterate_comp_subset hs 1

/-!
### Balls in uniform spaces
-/

namespace UniformSpace

open UniformSpace (ball)

lemma isOpen_ball (x : Î±) {V : SetRel Î± Î±} (hV : IsOpen V) : IsOpen (ball x V) :=
  hV.preimage <| .prodMk_right _

lemma isClosed_ball (x : Î±) {V : SetRel Î± Î±} (hV : IsClosed V) : IsClosed (ball x V) :=
  hV.preimage <| .prodMk_right _

/-!
### Neighborhoods in uniform spaces
-/

theorem hasBasis_nhds_prod (x y : Î±) :
    HasBasis (ğ“ (x, y)) (fun s => s âˆˆ ğ“¤ Î± âˆ§ SetRel.IsSymm s) fun s => ball x s Ã—Ë¢ ball y s := by
  rw [nhds_prod_eq]
  apply (hasBasis_nhds x).prod_same_index (hasBasis_nhds y)
  rintro U V âŸ¨U_in, U_symmâŸ© âŸ¨V_in, V_symmâŸ©
  exact âŸ¨U âˆ© V, âŸ¨(ğ“¤ Î±).inter_sets U_in V_in, inferInstanceâŸ©, ball_inter_left x U V,
    ball_inter_right y U VâŸ©

end UniformSpace

open UniformSpace

theorem nhds_eq_uniformity_prod {a b : Î±} :
    ğ“ (a, b) =
      (ğ“¤ Î±).lift' fun s : SetRel Î± Î± => { y : Î± | (y, a) âˆˆ s } Ã—Ë¢ { y : Î± | (b, y) âˆˆ s } := by
  rw [nhds_prod_eq, nhds_nhds_eq_uniformity_uniformity_prod, lift_lift'_same_eq_lift']
  Â· exact fun s => monotone_const.set_prod monotone_preimage
  Â· refine fun t => Monotone.set_prod ?_ monotone_const
    exact monotone_preimage (f := fun y => (y, a))

theorem nhdset_of_mem_uniformity {d : SetRel Î± Î±} (s : SetRel Î± Î±) (hd : d âˆˆ ğ“¤ Î±) :
    âˆƒ t : SetRel Î± Î±, IsOpen t âˆ§ s âŠ† t âˆ§
      t âŠ† { p | âˆƒ x y, (p.1, x) âˆˆ d âˆ§ (x, y) âˆˆ s âˆ§ (y, p.2) âˆˆ d } := by
  let cl_d := { p : Î± Ã— Î± | âˆƒ x y, (p.1, x) âˆˆ d âˆ§ (x, y) âˆˆ s âˆ§ (y, p.2) âˆˆ d }
  have : âˆ€ p âˆˆ s, âˆƒ t, t âŠ† cl_d âˆ§ IsOpen t âˆ§ p âˆˆ t := fun âŸ¨x, yâŸ© hp =>
    mem_nhds_iff.mp <|
      show cl_d âˆˆ ğ“ (x, y) by
        rw [nhds_eq_uniformity_prod, mem_lift'_sets]
        Â· exact âŸ¨d, hd, fun âŸ¨a, bâŸ© âŸ¨ha, hbâŸ© => âŸ¨x, y, ha, hp, hbâŸ©âŸ©
        Â· exact fun _ _ h _ h' => âŸ¨h h'.1, h h'.2âŸ©
  choose t ht using this
  exact âŸ¨(â‹ƒ p : Î± Ã— Î±, â‹ƒ h : p âˆˆ s, t p h : SetRel Î± Î±),
    isOpen_iUnion fun p : Î± Ã— Î± => isOpen_iUnion fun hp => (ht p hp).right.left,
    fun âŸ¨a, bâŸ© hp => by
      simp only [mem_iUnion, Prod.exists]; exact âŸ¨a, b, hp, (ht (a, b) hp).right.rightâŸ©,
    iUnion_subset fun p => iUnion_subset fun hp => (ht p hp).leftâŸ©

/-- Entourages are neighborhoods of the diagonal. -/
theorem nhds_le_uniformity (x : Î±) : ğ“ (x, x) â‰¤ ğ“¤ Î± := by
  intro V V_in
  rcases comp_symm_mem_uniformity_sets V_in with âŸ¨w, w_in, w_symm, w_subâŸ©
  have : ball x w Ã—Ë¢ ball x w âˆˆ ğ“ (x, x) := by
    rw [nhds_prod_eq]
    exact prod_mem_prod (ball_mem_nhds x w_in) (ball_mem_nhds x w_in)
  apply mem_of_superset this
  rintro âŸ¨u, vâŸ© âŸ¨u_in, v_inâŸ©
  exact w_sub (mem_comp_of_mem_ball u_in v_in)

/-- Entourages are neighborhoods of the diagonal. -/
theorem iSup_nhds_le_uniformity : â¨† x : Î±, ğ“ (x, x) â‰¤ ğ“¤ Î± :=
  iSup_le nhds_le_uniformity

/-- Entourages are neighborhoods of the diagonal. -/
theorem nhdsSet_diagonal_le_uniformity : ğ“Ë¢ (diagonal Î±) â‰¤ ğ“¤ Î± :=
  (nhdsSet_diagonal Î±).trans_le iSup_nhds_le_uniformity

section

variable (Î±)

theorem UniformSpace.has_seq_basis [IsCountablyGenerated <| ğ“¤ Î±] :
    âˆƒ V : â„• â†’ SetRel Î± Î±, HasAntitoneBasis (ğ“¤ Î±) V âˆ§ âˆ€ n, SetRel.IsSymm (V n) :=
  let âŸ¨U, hsym, hbasisâŸ© := (@UniformSpace.hasBasis_symmetric Î± _).exists_antitone_subbasis
  âŸ¨U, hbasis, fun n => (hsym n).2âŸ©

end

/-!
### Closure and interior in uniform spaces
-/

theorem closure_eq_uniformity (s : Set <| Î± Ã— Î±) :
    closure s = â‹‚ V âˆˆ {V | V âˆˆ ğ“¤ Î± âˆ§ SetRel.IsSymm V}, V â—‹ s â—‹ V := by
  ext âŸ¨x, yâŸ©
  simp +contextual only
    [mem_closure_iff_nhds_basis (UniformSpace.hasBasis_nhds_prod x y), mem_iInter, mem_setOf_eq,
      and_imp, mem_comp_comp, â† mem_inter_iff, inter_comm, Set.Nonempty]

theorem uniformity_hasBasis_closed :
    HasBasis (ğ“¤ Î±) (fun V : SetRel Î± Î± => V âˆˆ ğ“¤ Î± âˆ§ IsClosed V) id := by
  refine Filter.hasBasis_self.2 fun t h => ?_
  rcases comp_comp_symm_mem_uniformity_sets h with âŸ¨w, w_in, w_symm, râŸ©
  refine âŸ¨closure w, mem_of_superset w_in subset_closure, isClosed_closure, ?_âŸ©
  refine Subset.trans ?_ r
  rw [closure_eq_uniformity]
  apply iInter_subset_of_subset
  apply iInter_subset
  exact âŸ¨w_in, w_symmâŸ©

theorem uniformity_eq_uniformity_closure : ğ“¤ Î± = (ğ“¤ Î±).lift' closure :=
  Eq.symm <| uniformity_hasBasis_closed.lift'_closure_eq_self fun _ => And.right

theorem Filter.HasBasis.uniformity_closure {p : Î¹ â†’ Prop} {U : Î¹ â†’ SetRel Î± Î±}
    (h : (ğ“¤ Î±).HasBasis p U) : (ğ“¤ Î±).HasBasis p fun i => closure (U i) :=
  (@uniformity_eq_uniformity_closure Î± _).symm â–¸ h.lift'_closure

/-- Closed entourages form a basis of the uniformity filter. -/
theorem uniformity_hasBasis_closure : HasBasis (ğ“¤ Î±) (fun V : SetRel Î± Î± => V âˆˆ ğ“¤ Î±) closure :=
  (ğ“¤ Î±).basis_sets.uniformity_closure

theorem closure_eq_inter_uniformity {t : SetRel Î± Î±} : closure t = â‹‚ d âˆˆ ğ“¤ Î±, d â—‹ (t â—‹ d) :=
  calc
    closure t = â‹‚ (V) (_ : V âˆˆ ğ“¤ Î± âˆ§ SetRel.IsSymm V), V â—‹ t â—‹ V := closure_eq_uniformity t
    _ = â‹‚ V âˆˆ ğ“¤ Î±, V â—‹ t â—‹ V :=
      Eq.symm <|
        UniformSpace.hasBasis_symmetric.biInter_mem fun _ _ hV => by dsimp at *; gcongr
    _ = â‹‚ V âˆˆ ğ“¤ Î±, V â—‹ (t â—‹ V) := by simp [SetRel.comp_assoc]

theorem uniformity_eq_uniformity_interior : ğ“¤ Î± = (ğ“¤ Î±).lift' interior :=
  le_antisymm
    (le_iInfâ‚‚ fun d hd => by
      let âŸ¨s, hs, hs_compâŸ© := comp3_mem_uniformity hd
      let âŸ¨t, ht, hst, ht_compâŸ© := nhdset_of_mem_uniformity s hs
      have : s âŠ† interior d :=
        calc
          s âŠ† t := hst
          _ âŠ† interior d :=
            ht.subset_interior_iff.mpr fun x (hx : x âˆˆ t) =>
              let âŸ¨x, y, hâ‚, hâ‚‚, hâ‚ƒâŸ© := ht_comp hx
              hs_comp âŸ¨x, hâ‚, y, hâ‚‚, hâ‚ƒâŸ©
      have : interior d âˆˆ ğ“¤ Î± := by filter_upwards [hs] using this
      simp [this])
    fun _ hs => ((ğ“¤ Î±).lift' interior).sets_of_superset (mem_lift' hs) interior_subset

theorem interior_mem_uniformity {s : SetRel Î± Î±} (hs : s âˆˆ ğ“¤ Î±) : interior s âˆˆ ğ“¤ Î± := by
  rw [uniformity_eq_uniformity_interior]; exact mem_lift' hs

theorem mem_uniformity_isClosed {s : SetRel Î± Î±} (h : s âˆˆ ğ“¤ Î±) : âˆƒ t âˆˆ ğ“¤ Î±, IsClosed t âˆ§ t âŠ† s :=
  let âŸ¨t, âŸ¨ht_mem, htcâŸ©, htsâŸ© := uniformity_hasBasis_closed.mem_iff.1 h
  âŸ¨t, ht_mem, htc, htsâŸ©

theorem isOpen_iff_isOpen_ball_subset {s : Set Î±} :
    IsOpen s â†” âˆ€ x âˆˆ s, âˆƒ V âˆˆ ğ“¤ Î±, IsOpen V âˆ§ ball x V âŠ† s := by
  rw [isOpen_iff_ball_subset]
  constructor <;> intro h x hx
  Â· obtain âŸ¨V, hV, hV'âŸ© := h x hx
    exact
      âŸ¨interior V, interior_mem_uniformity hV, isOpen_interior,
        (ball_mono interior_subset x).trans hV'âŸ©
  Â· obtain âŸ¨V, hV, -, hV'âŸ© := h x hx
    exact âŸ¨V, hV, hV'âŸ©

/-- The uniform neighborhoods of all points of a dense set cover the whole space. -/
theorem Dense.biUnion_uniformity_ball {s : Set Î±} {U : SetRel Î± Î±} (hs : Dense s) (hU : U âˆˆ ğ“¤ Î±) :
    â‹ƒ x âˆˆ s, ball x U = univ := by
  refine iUnionâ‚‚_eq_univ_iff.2 fun y => ?_
  rcases hs.inter_nhds_nonempty (mem_nhds_right y hU) with âŸ¨x, hxs, hxy : (x, y) âˆˆ UâŸ©
  exact âŸ¨x, hxs, hxyâŸ©

/-- The uniform neighborhoods of all points of a dense indexed collection cover the whole space. -/
lemma DenseRange.iUnion_uniformity_ball {Î¹ : Type*} {xs : Î¹ â†’ Î±}
    (xs_dense : DenseRange xs) {U : SetRel Î± Î±} (hU : U âˆˆ uniformity Î±) :
    â‹ƒ i, UniformSpace.ball (xs i) U = univ := by
  rw [â† biUnion_range (f := xs) (g := fun x â†¦ UniformSpace.ball x U)]
  exact Dense.biUnion_uniformity_ball xs_dense hU

/-!
### Uniformity bases
-/

/-- Open elements of `ğ“¤ Î±` form a basis of `ğ“¤ Î±`. -/
theorem uniformity_hasBasis_open : HasBasis (ğ“¤ Î±) (fun V : SetRel Î± Î± => V âˆˆ ğ“¤ Î± âˆ§ IsOpen V) id :=
  hasBasis_self.2 fun s hs =>
    âŸ¨interior s, interior_mem_uniformity hs, isOpen_interior, interior_subsetâŸ©

theorem Filter.HasBasis.mem_uniformity_iff {p : Î² â†’ Prop} {s : Î² â†’ SetRel Î± Î±}
    (h : (ğ“¤ Î±).HasBasis p s) {t : SetRel Î± Î±} :
    t âˆˆ ğ“¤ Î± â†” âˆƒ i, p i âˆ§ âˆ€ a b, (a, b) âˆˆ s i â†’ (a, b) âˆˆ t :=
  h.mem_iff.trans <| by simp only [Prod.forall, subset_def]

/-- Open elements `s : SetRel Î± Î±` of `ğ“¤ Î±` such that `(x, y) âˆˆ s â†” (y, x) âˆˆ s` form a basis
of `ğ“¤ Î±`. -/
theorem uniformity_hasBasis_open_symmetric :
    HasBasis (ğ“¤ Î±) (fun V : SetRel Î± Î± => V âˆˆ ğ“¤ Î± âˆ§ IsOpen V âˆ§ SetRel.IsSymm V) id := by
  simp only [â† and_assoc]
  refine uniformity_hasBasis_open.restrict fun s hs => âŸ¨SetRel.symmetrize s, ?_âŸ©
  exact
    âŸ¨âŸ¨symmetrize_mem_uniformity hs.1, IsOpen.inter hs.2 (hs.2.preimage continuous_swap)âŸ©,
      inferInstance, SetRel.symmetrize_subset_selfâŸ©

theorem comp_open_symm_mem_uniformity_sets {s : SetRel Î± Î±} (hs : s âˆˆ ğ“¤ Î±) :
    âˆƒ t âˆˆ ğ“¤ Î±, IsOpen t âˆ§ SetRel.IsSymm t âˆ§ t â—‹ t âŠ† s := by
  obtain âŸ¨t, htâ‚, htâ‚‚âŸ© := comp_mem_uniformity_sets hs
  obtain âŸ¨u, âŸ¨huâ‚, huâ‚‚, huâ‚ƒâŸ©, huâ‚„ : u âŠ† tâŸ© := uniformity_hasBasis_open_symmetric.mem_iff.mp htâ‚
  exact âŸ¨u, huâ‚, huâ‚‚, huâ‚ƒ, (SetRel.comp_subset_comp huâ‚„ huâ‚„).trans htâ‚‚âŸ©

end UniformSpace

open uniformity

section Constructions

instance : PartialOrder (UniformSpace Î±) :=
  PartialOrder.lift (fun u => ğ“¤[u]) fun _ _ => UniformSpace.ext

protected theorem UniformSpace.le_def {uâ‚ uâ‚‚ : UniformSpace Î±} : uâ‚ â‰¤ uâ‚‚ â†” ğ“¤[uâ‚] â‰¤ ğ“¤[uâ‚‚] := Iff.rfl

instance : InfSet (UniformSpace Î±) :=
  âŸ¨fun s =>
    UniformSpace.ofCore
      { uniformity := â¨… u âˆˆ s, ğ“¤[u]
        refl := le_iInf fun u => le_iInf fun _ => u.toCore.refl
        symm := le_iInfâ‚‚ fun u hu =>
          le_trans (map_mono <| iInf_le_of_le _ <| iInf_le _ hu) u.symm
        comp := le_iInfâ‚‚ fun u hu =>
          le_trans (lift'_mono (iInf_le_of_le _ <| iInf_le _ hu) <| le_rfl) u.comp }âŸ©

protected theorem UniformSpace.sInf_le {tt : Set (UniformSpace Î±)} {t : UniformSpace Î±}
    (h : t âˆˆ tt) : sInf tt â‰¤ t :=
  show â¨… u âˆˆ tt, ğ“¤[u] â‰¤ ğ“¤[t] from iInfâ‚‚_le t h

protected theorem UniformSpace.le_sInf {tt : Set (UniformSpace Î±)} {t : UniformSpace Î±}
    (h : âˆ€ t' âˆˆ tt, t â‰¤ t') : t â‰¤ sInf tt :=
  show ğ“¤[t] â‰¤ â¨… u âˆˆ tt, ğ“¤[u] from le_iInfâ‚‚ h

instance : Top (UniformSpace Î±) :=
  âŸ¨@UniformSpace.mk Î± âŠ¤ âŠ¤ le_top le_top fun x â†¦ by simp only [nhds_top, comap_top]âŸ©

instance : Bot (UniformSpace Î±) :=
  âŸ¨{  toTopologicalSpace := âŠ¥
      uniformity := ğ“Ÿ SetRel.id
      symm := by simp [Tendsto, SetRel.id]
      comp := lift'_le (mem_principal_self _) <| principal_mono.2 (SetRel.id_comp _).subset
      nhds_eq_comap_uniformity := fun s => by
        let _ : TopologicalSpace Î± := âŠ¥; have := discreteTopology_bot Î±
        simp [SetRel.id] }âŸ©

instance : Min (UniformSpace Î±) :=
  âŸ¨fun uâ‚ uâ‚‚ =>
    { uniformity := ğ“¤[uâ‚] âŠ“ ğ“¤[uâ‚‚]
      symm := uâ‚.symm.inf uâ‚‚.symm
      comp := (lift'_inf_le _ _ _).trans <| inf_le_inf uâ‚.comp uâ‚‚.comp
      toTopologicalSpace := uâ‚.toTopologicalSpace âŠ“ uâ‚‚.toTopologicalSpace
      nhds_eq_comap_uniformity := fun _ â†¦ by
        rw [@nhds_inf _ uâ‚.toTopologicalSpace _, @nhds_eq_comap_uniformity _ uâ‚,
          @nhds_eq_comap_uniformity _ uâ‚‚, comap_inf] }âŸ©

instance : CompleteLattice (UniformSpace Î±) :=
  { inferInstanceAs (PartialOrder (UniformSpace Î±)) with
    sup := fun a b => sInf { x | a â‰¤ x âˆ§ b â‰¤ x }
    le_sup_left := fun _ _ => UniformSpace.le_sInf fun _ âŸ¨h, _âŸ© => h
    le_sup_right := fun _ _ => UniformSpace.le_sInf fun _ âŸ¨_, hâŸ© => h
    sup_le := fun _ _ _ hâ‚ hâ‚‚ => UniformSpace.sInf_le âŸ¨hâ‚, hâ‚‚âŸ©
    inf := (Â· âŠ“ Â·)
    le_inf := fun a _ _ hâ‚ hâ‚‚ => show a.uniformity â‰¤ _ from le_inf hâ‚ hâ‚‚
    inf_le_left := fun a _ => show _ â‰¤ a.uniformity from inf_le_left
    inf_le_right := fun _ b => show _ â‰¤ b.uniformity from inf_le_right
    top := âŠ¤
    le_top := fun a => show a.uniformity â‰¤ âŠ¤ from le_top
    bot := âŠ¥
    bot_le := fun u => u.toCore.refl
    sSup := fun tt => sInf { t | âˆ€ t' âˆˆ tt, t' â‰¤ t }
    le_sSup := fun _ _ h => UniformSpace.le_sInf fun _ h' => h' _ h
    sSup_le := fun _ _ h => UniformSpace.sInf_le h
    sInf := sInf
    le_sInf := fun _ _ hs => UniformSpace.le_sInf hs
    sInf_le := fun _ _ ha => UniformSpace.sInf_le ha }

theorem iInf_uniformity {Î¹ : Sort*} {u : Î¹ â†’ UniformSpace Î±} : ğ“¤[iInf u] = â¨… i, ğ“¤[u i] :=
  iInf_range

theorem inf_uniformity {u v : UniformSpace Î±} : ğ“¤[u âŠ“ v] = ğ“¤[u] âŠ“ ğ“¤[v] := rfl

lemma bot_uniformity : ğ“¤[(âŠ¥ : UniformSpace Î±)] = ğ“Ÿ SetRel.id := rfl

lemma top_uniformity : ğ“¤[(âŠ¤ : UniformSpace Î±)] = âŠ¤ := rfl

instance inhabitedUniformSpace : Inhabited (UniformSpace Î±) :=
  âŸ¨âŠ¥âŸ©

instance inhabitedUniformSpaceCore : Inhabited (UniformSpace.Core Î±) :=
  âŸ¨@UniformSpace.toCore _ defaultâŸ©

instance [Subsingleton Î±] : Unique (UniformSpace Î±) where
  uniq u := bot_unique <| le_principal_iff.2 <| by
    rw [SetRel.id, â† diagonal, diagonal_eq_univ]; exact univ_mem

/-- Given `f : Î± â†’ Î²` and a uniformity `u` on `Î²`, the inverse image of `u` under `f`
  is the inverse image in the filter sense of the induced function `Î± Ã— Î± â†’ Î² Ã— Î²`.
  See note [reducible non-instances]. -/
abbrev UniformSpace.comap (f : Î± â†’ Î²) (u : UniformSpace Î²) : UniformSpace Î± where
  uniformity := ğ“¤[u].comap fun p : Î± Ã— Î± => (f p.1, f p.2)
  symm := by
    simp only [tendsto_comap_iff]
    exact tendsto_swap_uniformity.comp tendsto_comap
  comp := le_trans
    (by
      rw [comap_lift'_eq, comap_lift'_eq2]
      Â· exact lift'_mono' fun s _ âŸ¨aâ‚, aâ‚‚âŸ© âŸ¨x, hâ‚, hâ‚‚âŸ© => âŸ¨f x, hâ‚, hâ‚‚âŸ©
      Â· exact monotone_id.relComp monotone_id)
    (comap_mono u.comp)
  toTopologicalSpace := u.toTopologicalSpace.induced f
  nhds_eq_comap_uniformity x := by
    simp only [nhds_induced, nhds_eq_comap_uniformity, comap_comap, Function.comp_def]

theorem uniformity_comap {_ : UniformSpace Î²} (f : Î± â†’ Î²) :
    ğ“¤[UniformSpace.comap f â€¹_â€º] = comap (Prod.map f f) (ğ“¤ Î²) :=
  rfl

lemma ball_preimage {f : Î± â†’ Î²} {U : SetRel Î² Î²} {x : Î±} :
    UniformSpace.ball x (Prod.map f f â»Â¹' U) = f â»Â¹' UniformSpace.ball (f x) U := by
  ext : 1
  simp only [UniformSpace.ball, mem_preimage, Prod.map_apply]

@[simp]
theorem uniformSpace_comap_id {Î± : Type*} : UniformSpace.comap (id : Î± â†’ Î±) = id := by
  ext : 2
  rw [uniformity_comap, Prod.map_id, comap_id]

theorem UniformSpace.comap_comap {Î± Î² Î³} {uÎ³ : UniformSpace Î³} {f : Î± â†’ Î²} {g : Î² â†’ Î³} :
    UniformSpace.comap (g âˆ˜ f) uÎ³ = UniformSpace.comap f (UniformSpace.comap g uÎ³) := by
  ext1
  simp only [uniformity_comap, Filter.comap_comap, Prod.map_comp_map]

theorem UniformSpace.comap_inf {Î± Î³} {uâ‚ uâ‚‚ : UniformSpace Î³} {f : Î± â†’ Î³} :
    (uâ‚ âŠ“ uâ‚‚).comap f = uâ‚.comap f âŠ“ uâ‚‚.comap f :=
  UniformSpace.ext Filter.comap_inf

theorem UniformSpace.comap_iInf {Î¹ Î± Î³} {u : Î¹ â†’ UniformSpace Î³} {f : Î± â†’ Î³} :
    (â¨… i, u i).comap f = â¨… i, (u i).comap f := by
  ext : 1
  simp [uniformity_comap, iInf_uniformity]

theorem UniformSpace.comap_mono {Î± Î³} {f : Î± â†’ Î³} :
    Monotone fun u : UniformSpace Î³ => u.comap f := fun _ _ hu =>
  Filter.comap_mono hu

theorem uniformContinuous_iff {Î± Î²} {uÎ± : UniformSpace Î±} {uÎ² : UniformSpace Î²} {f : Î± â†’ Î²} :
    UniformContinuous f â†” uÎ± â‰¤ uÎ².comap f :=
  Filter.map_le_iff_le_comap

theorem le_iff_uniformContinuous_id {u v : UniformSpace Î±} :
    u â‰¤ v â†” @UniformContinuous _ _ u v id := by
  rw [uniformContinuous_iff, uniformSpace_comap_id, id]

theorem uniformContinuous_comap {f : Î± â†’ Î²} [u : UniformSpace Î²] :
    @UniformContinuous Î± Î² (UniformSpace.comap f u) u f :=
  tendsto_comap

theorem uniformContinuous_comap' {f : Î³ â†’ Î²} {g : Î± â†’ Î³} [v : UniformSpace Î²] [u : UniformSpace Î±]
    (h : UniformContinuous (f âˆ˜ g)) : @UniformContinuous Î± Î³ u (UniformSpace.comap f v) g :=
  tendsto_comap_iff.2 h

namespace UniformSpace

theorem to_nhds_mono {uâ‚ uâ‚‚ : UniformSpace Î±} (h : uâ‚ â‰¤ uâ‚‚) (a : Î±) :
    @nhds _ (@UniformSpace.toTopologicalSpace _ uâ‚) a â‰¤
      @nhds _ (@UniformSpace.toTopologicalSpace _ uâ‚‚) a := by
  rw [@nhds_eq_uniformity Î± uâ‚ a, @nhds_eq_uniformity Î± uâ‚‚ a]; exact lift'_mono h le_rfl

theorem toTopologicalSpace_mono {uâ‚ uâ‚‚ : UniformSpace Î±} (h : uâ‚ â‰¤ uâ‚‚) :
    @UniformSpace.toTopologicalSpace _ uâ‚ â‰¤ @UniformSpace.toTopologicalSpace _ uâ‚‚ :=
  le_of_nhds_le_nhds <| to_nhds_mono h

theorem toTopologicalSpace_comap {f : Î± â†’ Î²} {u : UniformSpace Î²} :
    @UniformSpace.toTopologicalSpace _ (UniformSpace.comap f u) =
      TopologicalSpace.induced f (@UniformSpace.toTopologicalSpace Î² u) :=
  rfl

lemma uniformSpace_eq_bot {u : UniformSpace Î±} : u = âŠ¥ â†” SetRel.id âˆˆ ğ“¤[u] :=
  le_bot_iff.symm.trans le_principal_iff

protected lemma _root_.Filter.HasBasis.uniformSpace_eq_bot {Î¹ p} {s : Î¹ â†’ SetRel Î± Î±}
    {u : UniformSpace Î±} (h : ğ“¤[u].HasBasis p s) :
    u = âŠ¥ â†” âˆƒ i, p i âˆ§ Pairwise fun x y : Î± â†¦ (x, y) âˆ‰ s i := by
  simp [uniformSpace_eq_bot, h.mem_iff, subset_def, Pairwise, not_imp_not]

theorem toTopologicalSpace_bot : @UniformSpace.toTopologicalSpace Î± âŠ¥ = âŠ¥ := rfl

theorem toTopologicalSpace_top : @UniformSpace.toTopologicalSpace Î± âŠ¤ = âŠ¤ := rfl

theorem toTopologicalSpace_iInf {Î¹ : Sort*} {u : Î¹ â†’ UniformSpace Î±} :
    (iInf u).toTopologicalSpace = â¨… i, (u i).toTopologicalSpace :=
  TopologicalSpace.ext_nhds fun a â†¦ by simp only [@nhds_eq_comap_uniformity _ (iInf u), nhds_iInf,
    iInf_uniformity, @nhds_eq_comap_uniformity _ (u _), Filter.comap_iInf]

theorem toTopologicalSpace_sInf {s : Set (UniformSpace Î±)} :
    (sInf s).toTopologicalSpace = â¨… i âˆˆ s, @UniformSpace.toTopologicalSpace Î± i := by
  rw [sInf_eq_iInf]
  simp only [â† toTopologicalSpace_iInf]

theorem toTopologicalSpace_inf {u v : UniformSpace Î±} :
    (u âŠ“ v).toTopologicalSpace = u.toTopologicalSpace âŠ“ v.toTopologicalSpace :=
  rfl

end UniformSpace

section

variable [UniformSpace Î±] [UniformSpace Î²] [UniformSpace Î³] {f : Î± â†’ Î²} {s t : Set Î±}

theorem UniformContinuous.continuous (hf : UniformContinuous f) : Continuous f :=
  continuous_iff_le_induced.mpr <| UniformSpace.toTopologicalSpace_mono <|
    uniformContinuous_iff.1 hf

lemma UniformContinuous.uniformContinuousOn (hf : UniformContinuous f) :
    UniformContinuousOn f s :=
  tendsto_inf_left hf

lemma UniformContinuousOn.mono (hf : UniformContinuousOn f s) (ht : t âŠ† s) :
    UniformContinuousOn f t :=
  Tendsto.mono_left hf (inf_le_inf le_rfl (by simp [ht]))

lemma UniformContinuousOn.congr {f g : Î± â†’ Î²} {s : Set Î±}
    (hf : UniformContinuousOn f s) (h : EqOn f g s) :
    UniformContinuousOn g s := by
  apply hf.congr'
  apply EventuallyEq.filter_mono _ inf_le_right
  filter_upwards [mem_principal_self _] with âŸ¨a, bâŸ© âŸ¨ha, hbâŸ© using by simp [h ha, h hb]

lemma UniformContinuousOn.comp {g : Î² â†’ Î³} {t : Set Î²} (hg : UniformContinuousOn g t)
    (hf : UniformContinuousOn f s) (hst : MapsTo f s t) : UniformContinuousOn (g âˆ˜ f) s := by
  change Tendsto ((fun x â†¦ (g x.1, g x.2)) âˆ˜ (fun x â†¦ (f x.1, f x.2))) (ğ“¤ Î± âŠ“ ğ“Ÿ (s Ã—Ë¢ s)) (ğ“¤ Î³)
  apply Tendsto.comp hg
  refine tendsto_inf.2 âŸ¨hf, tendsto_inf_right ?_âŸ©
  simp only [tendsto_principal, mem_prod, eventually_principal, and_imp, Prod.forall]
  exact fun a b ha hb â†¦ âŸ¨hst ha, hst hbâŸ©

lemma UniformContinuous.comp_uniformContinuousOn {g : Î² â†’ Î³}
    (hg : UniformContinuous g) (hf : UniformContinuousOn f s) : UniformContinuousOn (g âˆ˜ f) s :=
  (hg.uniformContinuousOn (s := univ)).comp hf (mapsTo_univ _ _)

end

/-- Uniform space structure on `ULift Î±`. -/
instance ULift.uniformSpace [UniformSpace Î±] : UniformSpace (ULift Î±) :=
  UniformSpace.comap ULift.down â€¹_â€º

/-- Uniform space structure on `Î±áµ’áµˆ`. -/
instance OrderDual.instUniformSpace [UniformSpace Î±] : UniformSpace (Î±áµ’áµˆ) :=
  â€¹UniformSpace Î±â€º

section UniformContinuousInfi

-- TODO: add an `iff` lemma?
theorem UniformContinuous.inf_rng {f : Î± â†’ Î²} {uâ‚ : UniformSpace Î±} {uâ‚‚ uâ‚ƒ : UniformSpace Î²}
    (hâ‚ : UniformContinuous[uâ‚, uâ‚‚] f) (hâ‚‚ : UniformContinuous[uâ‚, uâ‚ƒ] f) :
    UniformContinuous[uâ‚, uâ‚‚ âŠ“ uâ‚ƒ] f :=
  tendsto_inf.mpr âŸ¨hâ‚, hâ‚‚âŸ©

theorem UniformContinuous.inf_dom_left {f : Î± â†’ Î²} {uâ‚ uâ‚‚ : UniformSpace Î±} {uâ‚ƒ : UniformSpace Î²}
    (hf : UniformContinuous[uâ‚, uâ‚ƒ] f) : UniformContinuous[uâ‚ âŠ“ uâ‚‚, uâ‚ƒ] f :=
  tendsto_inf_left hf

theorem UniformContinuous.inf_dom_right {f : Î± â†’ Î²} {uâ‚ uâ‚‚ : UniformSpace Î±} {uâ‚ƒ : UniformSpace Î²}
    (hf : UniformContinuous[uâ‚‚, uâ‚ƒ] f) : UniformContinuous[uâ‚ âŠ“ uâ‚‚, uâ‚ƒ] f :=
  tendsto_inf_right hf

theorem uniformContinuous_sInf_dom {f : Î± â†’ Î²} {uâ‚ : Set (UniformSpace Î±)} {uâ‚‚ : UniformSpace Î²}
    {u : UniformSpace Î±} (hâ‚ : u âˆˆ uâ‚) (hf : UniformContinuous[u, uâ‚‚] f) :
    UniformContinuous[sInf uâ‚, uâ‚‚] f := by
  delta UniformContinuous
  rw [sInf_eq_iInf', iInf_uniformity]
  exact tendsto_iInf' âŸ¨u, hâ‚âŸ© hf

theorem uniformContinuous_sInf_rng {f : Î± â†’ Î²} {uâ‚ : UniformSpace Î±} {uâ‚‚ : Set (UniformSpace Î²)} :
    UniformContinuous[uâ‚, sInf uâ‚‚] f â†” âˆ€ u âˆˆ uâ‚‚, UniformContinuous[uâ‚, u] f := by
  delta UniformContinuous
  rw [sInf_eq_iInf', iInf_uniformity, tendsto_iInf, SetCoe.forall]

theorem uniformContinuous_iInf_dom {f : Î± â†’ Î²} {uâ‚ : Î¹ â†’ UniformSpace Î±} {uâ‚‚ : UniformSpace Î²}
    {i : Î¹} (hf : UniformContinuous[uâ‚ i, uâ‚‚] f) : UniformContinuous[iInf uâ‚, uâ‚‚] f := by
  delta UniformContinuous
  rw [iInf_uniformity]
  exact tendsto_iInf' i hf

theorem uniformContinuous_iInf_rng {f : Î± â†’ Î²} {uâ‚ : UniformSpace Î±} {uâ‚‚ : Î¹ â†’ UniformSpace Î²} :
    UniformContinuous[uâ‚, iInf uâ‚‚] f â†” âˆ€ i, UniformContinuous[uâ‚, uâ‚‚ i] f := by
  delta UniformContinuous
  rw [iInf_uniformity, tendsto_iInf]

end UniformContinuousInfi

/-- A uniform space with the discrete uniformity has the discrete topology. -/
theorem discreteTopology_of_discrete_uniformity [hÎ± : UniformSpace Î±]
    (h : uniformity Î± = ğ“Ÿ SetRel.id) : DiscreteTopology Î± :=
  âŸ¨(UniformSpace.ext h.symm : âŠ¥ = hÎ±) â–¸ rflâŸ©

instance : UniformSpace Empty := âŠ¥
instance : UniformSpace PUnit := âŠ¥
instance : UniformSpace Bool := âŠ¥
instance : UniformSpace â„• := âŠ¥
instance : UniformSpace â„¤ := âŠ¥

section

variable [UniformSpace Î±]

open Additive Multiplicative

instance : UniformSpace (Additive Î±) := â€¹UniformSpace Î±â€º
instance : UniformSpace (Multiplicative Î±) := â€¹UniformSpace Î±â€º

theorem uniformContinuous_ofMul : UniformContinuous (ofMul : Î± â†’ Additive Î±) :=
  uniformContinuous_id

theorem uniformContinuous_toMul : UniformContinuous (toMul : Additive Î± â†’ Î±) :=
  uniformContinuous_id

theorem uniformContinuous_ofAdd : UniformContinuous (ofAdd : Î± â†’ Multiplicative Î±) :=
  uniformContinuous_id

theorem uniformContinuous_toAdd : UniformContinuous (toAdd : Multiplicative Î± â†’ Î±) :=
  uniformContinuous_id

theorem uniformity_additive : ğ“¤ (Additive Î±) = (ğ“¤ Î±).map (Prod.map ofMul ofMul) := rfl

theorem uniformity_multiplicative : ğ“¤ (Multiplicative Î±) = (ğ“¤ Î±).map (Prod.map ofAdd ofAdd) := rfl

end

instance instUniformSpaceSubtype {p : Î± â†’ Prop} [t : UniformSpace Î±] : UniformSpace (Subtype p) :=
  UniformSpace.comap Subtype.val t

theorem uniformity_subtype {p : Î± â†’ Prop} [UniformSpace Î±] :
    ğ“¤ (Subtype p) = comap (fun q : Subtype p Ã— Subtype p => (q.1.1, q.2.1)) (ğ“¤ Î±) :=
  rfl

theorem uniformity_setCoe {s : Set Î±} [UniformSpace Î±] :
    ğ“¤ s = comap (Prod.map ((â†‘) : s â†’ Î±) ((â†‘) : s â†’ Î±)) (ğ“¤ Î±) :=
  rfl

theorem map_uniformity_set_coe {s : Set Î±} [UniformSpace Î±] :
    map (Prod.map (â†‘) (â†‘)) (ğ“¤ s) = ğ“¤ Î± âŠ“ ğ“Ÿ (s Ã—Ë¢ s) := by
  rw [uniformity_setCoe, map_comap, range_prodMap, Subtype.range_val]

theorem uniformContinuous_subtype_val {p : Î± â†’ Prop} [UniformSpace Î±] :
    UniformContinuous (Subtype.val : { a : Î± // p a } â†’ Î±) :=
  uniformContinuous_comap

theorem UniformContinuous.subtype_mk {p : Î± â†’ Prop} [UniformSpace Î±] [UniformSpace Î²] {f : Î² â†’ Î±}
    (hf : UniformContinuous f) (h : âˆ€ x, p (f x)) :
    UniformContinuous (fun x => âŸ¨f x, h xâŸ© : Î² â†’ Subtype p) :=
  uniformContinuous_comap' hf

theorem UniformContinuous.subtype_map [UniformSpace Î±] [UniformSpace Î²] {p : Î± â†’ Prop}
    {q : Î² â†’ Prop} {f : Î± â†’ Î²} (hf : UniformContinuous f) (h : âˆ€ x, p x â†’ q (f x)) :
    UniformContinuous (Subtype.map f h) :=
  (hf.comp uniformContinuous_subtype_val).subtype_mk _

theorem uniformContinuousOn_iff_restrict [UniformSpace Î±] [UniformSpace Î²] {f : Î± â†’ Î²} {s : Set Î±} :
    UniformContinuousOn f s â†” UniformContinuous (s.restrict f) := by
  delta UniformContinuousOn UniformContinuous
  rw [â† map_uniformity_set_coe, tendsto_map'_iff]; rfl

theorem tendsto_of_uniformContinuous_subtype [UniformSpace Î±] [UniformSpace Î²] {f : Î± â†’ Î²}
    {s : Set Î±} {a : Î±} (hf : UniformContinuous fun x : s => f x.val) (ha : s âˆˆ ğ“ a) :
    Tendsto f (ğ“ a) (ğ“ (f a)) := by
  rw [(@map_nhds_subtype_coe_eq_nhds Î± _ s a (mem_of_mem_nhds ha) ha).symm]
  exact tendsto_map' hf.continuous.continuousAt

theorem UniformContinuousOn.continuousOn [UniformSpace Î±] [UniformSpace Î²] {f : Î± â†’ Î²} {s : Set Î±}
    (h : UniformContinuousOn f s) : ContinuousOn f s := by
  rw [uniformContinuousOn_iff_restrict] at h
  rw [continuousOn_iff_continuous_restrict]
  exact h.continuous

@[to_additive]
instance [UniformSpace Î±] : UniformSpace Î±áµáµ’áµ– :=
  UniformSpace.comap MulOpposite.unop â€¹_â€º

@[to_additive]
theorem uniformity_mulOpposite [UniformSpace Î±] :
    ğ“¤ Î±áµáµ’áµ– = comap (fun q : Î±áµáµ’áµ– Ã— Î±áµáµ’áµ– => (q.1.unop, q.2.unop)) (ğ“¤ Î±) :=
  rfl

@[to_additive (attr := simp)]
theorem comap_uniformity_mulOpposite [UniformSpace Î±] :
    comap (fun p : Î± Ã— Î± => (MulOpposite.op p.1, MulOpposite.op p.2)) (ğ“¤ Î±áµáµ’áµ–) = ğ“¤ Î± := by
  simpa [uniformity_mulOpposite, comap_comap, (Â· âˆ˜ Â·)] using comap_id

namespace MulOpposite

@[to_additive]
theorem uniformContinuous_unop [UniformSpace Î±] : UniformContinuous (unop : Î±áµáµ’áµ– â†’ Î±) :=
  uniformContinuous_comap

@[to_additive]
theorem uniformContinuous_op [UniformSpace Î±] : UniformContinuous (op : Î± â†’ Î±áµáµ’áµ–) :=
  uniformContinuous_comap' uniformContinuous_id

end MulOpposite

section Prod

open UniformSpace

/- a similar product space is possible on the function space (uniformity of pointwise convergence),
  but we want to have the uniformity of uniform convergence on function spaces -/
instance instUniformSpaceProd [uâ‚ : UniformSpace Î±] [uâ‚‚ : UniformSpace Î²] : UniformSpace (Î± Ã— Î²) :=
  uâ‚.comap Prod.fst âŠ“ uâ‚‚.comap Prod.snd

-- check the above produces no diamond for `simp` and typeclass search
example [UniformSpace Î±] [UniformSpace Î²] :
    (instTopologicalSpaceProd : TopologicalSpace (Î± Ã— Î²)) = UniformSpace.toTopologicalSpace := by
  with_reducible_and_instances rfl

theorem uniformity_prod [UniformSpace Î±] [UniformSpace Î²] :
    ğ“¤ (Î± Ã— Î²) =
      ((ğ“¤ Î±).comap fun p : (Î± Ã— Î²) Ã— Î± Ã— Î² => (p.1.1, p.2.1)) âŠ“
        (ğ“¤ Î²).comap fun p : (Î± Ã— Î²) Ã— Î± Ã— Î² => (p.1.2, p.2.2) :=
  rfl

instance [UniformSpace Î±] [IsCountablyGenerated (ğ“¤ Î±)]
    [UniformSpace Î²] [IsCountablyGenerated (ğ“¤ Î²)] : IsCountablyGenerated (ğ“¤ (Î± Ã— Î²)) := by
  rw [uniformity_prod]
  infer_instance

theorem uniformity_prod_eq_comap_prod [UniformSpace Î±] [UniformSpace Î²] :
    ğ“¤ (Î± Ã— Î²) =
      comap (fun p : (Î± Ã— Î²) Ã— Î± Ã— Î² => ((p.1.1, p.2.1), (p.1.2, p.2.2))) (ğ“¤ Î± Ã—Ë¢ ğ“¤ Î²) := by
  simp_rw [uniformity_prod, prod_eq_inf, Filter.comap_inf, Filter.comap_comap, Function.comp_def]

theorem uniformity_prod_eq_prod [UniformSpace Î±] [UniformSpace Î²] :
    ğ“¤ (Î± Ã— Î²) = map (fun p : (Î± Ã— Î±) Ã— Î² Ã— Î² => ((p.1.1, p.2.1), (p.1.2, p.2.2))) (ğ“¤ Î± Ã—Ë¢ ğ“¤ Î²) := by
  rw [map_swap4_eq_comap, uniformity_prod_eq_comap_prod]

theorem mem_uniformity_of_uniformContinuous_invariant [UniformSpace Î±] [UniformSpace Î²]
    {s : SetRel Î² Î²} {f : Î± â†’ Î± â†’ Î²} (hf : UniformContinuous fun p : Î± Ã— Î± => f p.1 p.2)
    (hs : s âˆˆ ğ“¤ Î²) : âˆƒ u âˆˆ ğ“¤ Î±, âˆ€ a b c, (a, b) âˆˆ u â†’ (f a c, f b c) âˆˆ s := by
  rw [UniformContinuous, uniformity_prod_eq_prod, tendsto_map'_iff] at hf
  rcases mem_prod_iff.1 (mem_map.1 <| hf hs) with âŸ¨u, hu, v, hv, huvtâŸ©
  exact âŸ¨u, hu, fun a b c hab => @huvt ((_, _), (_, _)) âŸ¨hab, refl_mem_uniformity hvâŸ©âŸ©

/-- An entourage of the diagonal in `Î±` and an entourage in `Î²` yield an entourage in `Î± Ã— Î²`
once we permute coordinates. -/
def entourageProd (u : SetRel Î± Î±) (v : SetRel Î² Î²) : SetRel (Î± Ã— Î²) (Î± Ã— Î²) :=
  {((aâ‚, bâ‚),(aâ‚‚, bâ‚‚)) | (aâ‚, aâ‚‚) âˆˆ u âˆ§ (bâ‚, bâ‚‚) âˆˆ v}

theorem mem_entourageProd {u : SetRel Î± Î±} {v : SetRel Î² Î²} {p : (Î± Ã— Î²) Ã— Î± Ã— Î²} :
    p âˆˆ entourageProd u v â†” (p.1.1, p.2.1) âˆˆ u âˆ§ (p.1.2, p.2.2) âˆˆ v := Iff.rfl

theorem entourageProd_mem_uniformity [tâ‚ : UniformSpace Î±] [tâ‚‚ : UniformSpace Î²] {u : SetRel Î± Î±}
    {v : SetRel Î² Î²} (hu : u âˆˆ ğ“¤ Î±) (hv : v âˆˆ ğ“¤ Î²) :
    entourageProd u v âˆˆ ğ“¤ (Î± Ã— Î²) := by
  rw [uniformity_prod]; exact inter_mem_inf (preimage_mem_comap hu) (preimage_mem_comap hv)

theorem ball_entourageProd (u : SetRel Î± Î±) (v : SetRel Î² Î²) (x : Î± Ã— Î²) :
    ball x (entourageProd u v) = ball x.1 u Ã—Ë¢ ball x.2 v := by
  ext p; simp only [ball, entourageProd, Set.mem_setOf_eq, Set.mem_prod, Set.mem_preimage]

instance IsSymm_entourageProd {u : SetRel Î± Î±} {v : SetRel Î² Î²} [u.IsSymm] [v.IsSymm] :
    (entourageProd u v).IsSymm where
  symm _ _ := .imp u.symm v.symm

theorem Filter.HasBasis.uniformity_prod {Î¹a Î¹b : Type*} [UniformSpace Î±] [UniformSpace Î²]
    {pa : Î¹a â†’ Prop} {pb : Î¹b â†’ Prop} {sa : Î¹a â†’ SetRel Î± Î±} {sb : Î¹b â†’ SetRel Î² Î²}
    (ha : (ğ“¤ Î±).HasBasis pa sa) (hb : (ğ“¤ Î²).HasBasis pb sb) :
    (ğ“¤ (Î± Ã— Î²)).HasBasis (fun i : Î¹a Ã— Î¹b â†¦ pa i.1 âˆ§ pb i.2)
    (fun i â†¦ entourageProd (sa i.1) (sb i.2)) :=
  (ha.comap _).inf (hb.comap _)

theorem entourageProd_subset [UniformSpace Î±] [UniformSpace Î²]
    {s : Set ((Î± Ã— Î²) Ã— Î± Ã— Î²)} (h : s âˆˆ ğ“¤ (Î± Ã— Î²)) :
    âˆƒ u âˆˆ ğ“¤ Î±, âˆƒ v âˆˆ ğ“¤ Î², entourageProd u v âŠ† s := by
  rcases (((ğ“¤ Î±).basis_sets.uniformity_prod (ğ“¤ Î²).basis_sets).mem_iff' s).1 h with âŸ¨w, hwâŸ©
  use w.1, hw.1.1, w.2, hw.1.2, hw.2

theorem tendsto_prod_uniformity_fst [UniformSpace Î±] [UniformSpace Î²] :
    Tendsto (fun p : (Î± Ã— Î²) Ã— Î± Ã— Î² => (p.1.1, p.2.1)) (ğ“¤ (Î± Ã— Î²)) (ğ“¤ Î±) :=
  le_trans (map_mono inf_le_left) map_comap_le

theorem tendsto_prod_uniformity_snd [UniformSpace Î±] [UniformSpace Î²] :
    Tendsto (fun p : (Î± Ã— Î²) Ã— Î± Ã— Î² => (p.1.2, p.2.2)) (ğ“¤ (Î± Ã— Î²)) (ğ“¤ Î²) :=
  le_trans (map_mono inf_le_right) map_comap_le

theorem uniformContinuous_fst [UniformSpace Î±] [UniformSpace Î²] :
    UniformContinuous fun p : Î± Ã— Î² => p.1 :=
  tendsto_prod_uniformity_fst

theorem uniformContinuous_snd [UniformSpace Î±] [UniformSpace Î²] :
    UniformContinuous fun p : Î± Ã— Î² => p.2 :=
  tendsto_prod_uniformity_snd

variable [UniformSpace Î±] [UniformSpace Î²] [UniformSpace Î³]

theorem UniformContinuous.prodMk {fâ‚ : Î± â†’ Î²} {fâ‚‚ : Î± â†’ Î³} (hâ‚ : UniformContinuous fâ‚)
    (hâ‚‚ : UniformContinuous fâ‚‚) : UniformContinuous fun a => (fâ‚ a, fâ‚‚ a) := by
  rw [UniformContinuous, uniformity_prod]
  exact tendsto_inf.2 âŸ¨tendsto_comap_iff.2 hâ‚, tendsto_comap_iff.2 hâ‚‚âŸ©

@[deprecated (since := "2025-03-10")]
alias UniformContinuous.prod_mk := UniformContinuous.prodMk

theorem UniformContinuous.prodMk_left {f : Î± Ã— Î² â†’ Î³} (h : UniformContinuous f) (b) :
    UniformContinuous fun a => f (a, b) :=
  h.comp (uniformContinuous_id.prodMk uniformContinuous_const)

@[deprecated (since := "2025-03-10")]
alias UniformContinuous.prod_mk_left := UniformContinuous.prodMk_left

theorem UniformContinuous.prodMk_right {f : Î± Ã— Î² â†’ Î³} (h : UniformContinuous f) (a) :
    UniformContinuous fun b => f (a, b) :=
  h.comp (uniformContinuous_const.prodMk uniformContinuous_id)

@[deprecated (since := "2025-03-10")]
alias UniformContinuous.prod_mk_right := UniformContinuous.prodMk_right

theorem UniformContinuous.prodMap [UniformSpace Î´] {f : Î± â†’ Î³} {g : Î² â†’ Î´}
    (hf : UniformContinuous f) (hg : UniformContinuous g) : UniformContinuous (Prod.map f g) :=
  (hf.comp uniformContinuous_fst).prodMk (hg.comp uniformContinuous_snd)

lemma uniformContinuous_swap :
    UniformContinuous (Prod.swap : Î± Ã— Î² â†’ Î² Ã— Î±) :=
  uniformContinuous_snd.prodMk uniformContinuous_fst

theorem toTopologicalSpace_prod {Î±} {Î²} [u : UniformSpace Î±] [v : UniformSpace Î²] :
    @UniformSpace.toTopologicalSpace (Î± Ã— Î²) instUniformSpaceProd =
      @instTopologicalSpaceProd Î± Î² u.toTopologicalSpace v.toTopologicalSpace :=
  rfl

/-- A version of `UniformContinuous.inf_dom_left` for binary functions -/
theorem uniformContinuous_inf_dom_leftâ‚‚ {Î± Î² Î³} {f : Î± â†’ Î² â†’ Î³} {ua1 ua2 : UniformSpace Î±}
    {ub1 ub2 : UniformSpace Î²} {uc1 : UniformSpace Î³}
    (h : by haveI := ua1; haveI := ub1; exact UniformContinuous fun p : Î± Ã— Î² => f p.1 p.2) : by
      haveI := ua1 âŠ“ ua2; haveI := ub1 âŠ“ ub2
      exact UniformContinuous fun p : Î± Ã— Î² => f p.1 p.2 := by
  -- proof essentially copied from `continuous_inf_dom_leftâ‚‚`
  have ha := @UniformContinuous.inf_dom_left _ _ id ua1 ua2 ua1 (@uniformContinuous_id _ (id _))
  have hb := @UniformContinuous.inf_dom_left _ _ id ub1 ub2 ub1 (@uniformContinuous_id _ (id _))
  have h_unif_cont_id :=
    @UniformContinuous.prodMap _ _ _ _ (ua1 âŠ“ ua2) (ub1 âŠ“ ub2) ua1 ub1 _ _ ha hb
  exact @UniformContinuous.comp _ _ _ (id _) (id _) _ _ _ h h_unif_cont_id

/-- A version of `UniformContinuous.inf_dom_right` for binary functions -/
theorem uniformContinuous_inf_dom_rightâ‚‚ {Î± Î² Î³} {f : Î± â†’ Î² â†’ Î³} {ua1 ua2 : UniformSpace Î±}
    {ub1 ub2 : UniformSpace Î²} {uc1 : UniformSpace Î³}
    (h : by haveI := ua2; haveI := ub2; exact UniformContinuous fun p : Î± Ã— Î² => f p.1 p.2) : by
      haveI := ua1 âŠ“ ua2; haveI := ub1 âŠ“ ub2
      exact UniformContinuous fun p : Î± Ã— Î² => f p.1 p.2 := by
  -- proof essentially copied from `continuous_inf_dom_rightâ‚‚`
  have ha := @UniformContinuous.inf_dom_right _ _ id ua1 ua2 ua2 (@uniformContinuous_id _ (id _))
  have hb := @UniformContinuous.inf_dom_right _ _ id ub1 ub2 ub2 (@uniformContinuous_id _ (id _))
  have h_unif_cont_id :=
    @UniformContinuous.prodMap _ _ _ _ (ua1 âŠ“ ua2) (ub1 âŠ“ ub2) ua2 ub2 _ _ ha hb
  exact @UniformContinuous.comp _ _ _ (id _) (id _) _ _ _ h h_unif_cont_id

/-- A version of `uniformContinuous_sInf_dom` for binary functions -/
theorem uniformContinuous_sInf_domâ‚‚ {Î± Î² Î³} {f : Î± â†’ Î² â†’ Î³} {uas : Set (UniformSpace Î±)}
    {ubs : Set (UniformSpace Î²)} {ua : UniformSpace Î±} {ub : UniformSpace Î²} {uc : UniformSpace Î³}
    (ha : ua âˆˆ uas) (hb : ub âˆˆ ubs) (hf : UniformContinuous fun p : Î± Ã— Î² => f p.1 p.2) : by
      haveI := sInf uas; haveI := sInf ubs
      exact @UniformContinuous _ _ _ uc fun p : Î± Ã— Î² => f p.1 p.2 := by
  -- proof essentially copied from `continuous_sInf_dom`
  let _ : UniformSpace (Î± Ã— Î²) := instUniformSpaceProd
  have ha := uniformContinuous_sInf_dom ha uniformContinuous_id
  have hb := uniformContinuous_sInf_dom hb uniformContinuous_id
  have h_unif_cont_id := @UniformContinuous.prodMap _ _ _ _ (sInf uas) (sInf ubs) ua ub _ _ ha hb
  exact @UniformContinuous.comp _ _ _ (id _) (id _) _ _ _ hf h_unif_cont_id

end Prod

section

open UniformSpace Function

variable {Î´' : Type*} [UniformSpace Î±] [UniformSpace Î²] [UniformSpace Î³] [UniformSpace Î´]
  [UniformSpace Î´']
local notation f " âˆ˜â‚‚ " g => Function.bicompr f g

/-- Uniform continuity for functions of two variables. -/
def UniformContinuousâ‚‚ (f : Î± â†’ Î² â†’ Î³) :=
  UniformContinuous (uncurry f)

theorem uniformContinuousâ‚‚_def (f : Î± â†’ Î² â†’ Î³) :
    UniformContinuousâ‚‚ f â†” UniformContinuous (uncurry f) :=
  Iff.rfl

theorem UniformContinuousâ‚‚.uniformContinuous {f : Î± â†’ Î² â†’ Î³} (h : UniformContinuousâ‚‚ f) :
    UniformContinuous (uncurry f) :=
  h

theorem uniformContinuousâ‚‚_curry (f : Î± Ã— Î² â†’ Î³) :
    UniformContinuousâ‚‚ (Function.curry f) â†” UniformContinuous f := by
  rw [UniformContinuousâ‚‚, uncurry_curry]

theorem UniformContinuousâ‚‚.comp {f : Î± â†’ Î² â†’ Î³} {g : Î³ â†’ Î´} (hg : UniformContinuous g)
    (hf : UniformContinuousâ‚‚ f) : UniformContinuousâ‚‚ (g âˆ˜â‚‚ f) :=
  hg.comp hf

theorem UniformContinuousâ‚‚.bicompl {f : Î± â†’ Î² â†’ Î³} {ga : Î´ â†’ Î±} {gb : Î´' â†’ Î²}
    (hf : UniformContinuousâ‚‚ f) (hga : UniformContinuous ga) (hgb : UniformContinuous gb) :
    UniformContinuousâ‚‚ (bicompl f ga gb) :=
  hf.uniformContinuous.comp (hga.prodMap hgb)

end

theorem toTopologicalSpace_subtype [u : UniformSpace Î±] {p : Î± â†’ Prop} :
    @UniformSpace.toTopologicalSpace (Subtype p) instUniformSpaceSubtype =
      @instTopologicalSpaceSubtype Î± p u.toTopologicalSpace :=
  rfl

section Sum

variable [UniformSpace Î±] [UniformSpace Î²]

open Sum

-- Obsolete auxiliary definitions and lemmas

/-- Uniformity on a disjoint union. Entourages of the diagonal in the union are obtained
by taking independently an entourage of the diagonal in the first part, and an entourage of
the diagonal in the second part. -/
instance Sum.instUniformSpace : UniformSpace (Î± âŠ• Î²) where
  uniformity := map (fun p : Î± Ã— Î± => (inl p.1, inl p.2)) (ğ“¤ Î±) âŠ”
    map (fun p : Î² Ã— Î² => (inr p.1, inr p.2)) (ğ“¤ Î²)
  symm := fun _ hs â†¦ âŸ¨symm_le_uniformity hs.1, symm_le_uniformity hs.2âŸ©
  comp := fun s hs â†¦ by
    rcases comp_mem_uniformity_sets hs.1 with âŸ¨tÎ±, htÎ±, HtÎ±âŸ©
    rcases comp_mem_uniformity_sets hs.2 with âŸ¨tÎ², htÎ², HtÎ²âŸ©
    filter_upwards [mem_lift' (union_mem_sup (image_mem_map htÎ±) (image_mem_map htÎ²))]
    rintro âŸ¨_, _âŸ© âŸ¨z, âŸ¨âŸ¨a, bâŸ©, hab, âŸ¨âŸ©âŸ© | âŸ¨âŸ¨a, bâŸ©, hab, âŸ¨âŸ©âŸ©, âŸ¨âŸ¨_, câŸ©, hbc, âŸ¨âŸ©âŸ© | âŸ¨âŸ¨_, câŸ©, hbc, âŸ¨âŸ©âŸ©âŸ©
    exacts [@HtÎ± (_, _) âŸ¨b, hab, hbcâŸ©, @HtÎ² (_, _) âŸ¨b, hab, hbcâŸ©]
  nhds_eq_comap_uniformity x := by
    ext
    cases x <;> simp [mem_comap', -mem_comap, nhds_inl, nhds_inr, nhds_eq_comap_uniformity,
      Prod.ext_iff]

/-- The union of an entourage of the diagonal in each set of a disjoint union is again an entourage
of the diagonal. -/
theorem union_mem_uniformity_sum {a : SetRel Î± Î±} (ha : a âˆˆ ğ“¤ Î±) {b : SetRel Î² Î²} (hb : b âˆˆ ğ“¤ Î²) :
    Prod.map inl inl '' a âˆª Prod.map inr inr '' b âˆˆ ğ“¤ (Î± âŠ• Î²) :=
  union_mem_sup (image_mem_map ha) (image_mem_map hb)

theorem Sum.uniformity : ğ“¤ (Î± âŠ• Î²) = map (Prod.map inl inl) (ğ“¤ Î±) âŠ” map (Prod.map inr inr) (ğ“¤ Î²) :=
  rfl

lemma uniformContinuous_inl : UniformContinuous (Sum.inl : Î± â†’ Î± âŠ• Î²) := le_sup_left
lemma uniformContinuous_inr : UniformContinuous (Sum.inr : Î² â†’ Î± âŠ• Î²) := le_sup_right

instance [IsCountablyGenerated (ğ“¤ Î±)] [IsCountablyGenerated (ğ“¤ Î²)] :
    IsCountablyGenerated (ğ“¤ (Î± âŠ• Î²)) := by
  rw [Sum.uniformity]
  infer_instance

end Sum

end Constructions

/-!
### Expressing continuity properties in uniform spaces

We reformulate the various continuity properties of functions taking values in a uniform space
in terms of the uniformity in the target. Since the same lemmas (essentially with the same names)
also exist for metric spaces and emetric spaces (reformulating things in terms of the distance or
the edistance in the target), we put them in a namespace `Uniform` here.

In the metric and emetric space setting, there are also similar lemmas where one assumes that
both the source and the target are metric spaces, reformulating things in terms of the distance
on both sides. These lemmas are generally written without primes, and the versions where only
the target is a metric space is primed. We follow the same convention here, thus giving lemmas
with primes.
-/


namespace Uniform

variable [UniformSpace Î±]

theorem tendsto_nhds_right {f : Filter Î²} {u : Î² â†’ Î±} {a : Î±} :
    Tendsto u f (ğ“ a) â†” Tendsto (fun x => (a, u x)) f (ğ“¤ Î±) := by
  rw [nhds_eq_comap_uniformity, tendsto_comap_iff]; rfl

theorem tendsto_nhds_left {f : Filter Î²} {u : Î² â†’ Î±} {a : Î±} :
    Tendsto u f (ğ“ a) â†” Tendsto (fun x => (u x, a)) f (ğ“¤ Î±) := by
  rw [nhds_eq_comap_uniformity', tendsto_comap_iff]; rfl

theorem continuousAt_iff'_right [TopologicalSpace Î²] {f : Î² â†’ Î±} {b : Î²} :
    ContinuousAt f b â†” Tendsto (fun x => (f b, f x)) (ğ“ b) (ğ“¤ Î±) := by
  rw [ContinuousAt, tendsto_nhds_right]

theorem continuousAt_iff'_left [TopologicalSpace Î²] {f : Î² â†’ Î±} {b : Î²} :
    ContinuousAt f b â†” Tendsto (fun x => (f x, f b)) (ğ“ b) (ğ“¤ Î±) := by
  rw [ContinuousAt, tendsto_nhds_left]

theorem continuousAt_iff_prod [TopologicalSpace Î²] {f : Î² â†’ Î±} {b : Î²} :
    ContinuousAt f b â†” Tendsto (fun x : Î² Ã— Î² => (f x.1, f x.2)) (ğ“ (b, b)) (ğ“¤ Î±) :=
  âŸ¨fun H => le_trans (H.prodMap' H) (nhds_le_uniformity _), fun H =>
    continuousAt_iff'_left.2 <| H.comp <| tendsto_id.prodMk_nhds tendsto_const_nhdsâŸ©

theorem continuousWithinAt_iff'_right [TopologicalSpace Î²] {f : Î² â†’ Î±} {b : Î²} {s : Set Î²} :
    ContinuousWithinAt f s b â†” Tendsto (fun x => (f b, f x)) (ğ“[s] b) (ğ“¤ Î±) := by
  rw [ContinuousWithinAt, tendsto_nhds_right]

theorem continuousWithinAt_iff'_left [TopologicalSpace Î²] {f : Î² â†’ Î±} {b : Î²} {s : Set Î²} :
    ContinuousWithinAt f s b â†” Tendsto (fun x => (f x, f b)) (ğ“[s] b) (ğ“¤ Î±) := by
  rw [ContinuousWithinAt, tendsto_nhds_left]

theorem continuousOn_iff'_right [TopologicalSpace Î²] {f : Î² â†’ Î±} {s : Set Î²} :
    ContinuousOn f s â†” âˆ€ b âˆˆ s, Tendsto (fun x => (f b, f x)) (ğ“[s] b) (ğ“¤ Î±) := by
  simp [ContinuousOn, continuousWithinAt_iff'_right]

theorem continuousOn_iff'_left [TopologicalSpace Î²] {f : Î² â†’ Î±} {s : Set Î²} :
    ContinuousOn f s â†” âˆ€ b âˆˆ s, Tendsto (fun x => (f x, f b)) (ğ“[s] b) (ğ“¤ Î±) := by
  simp [ContinuousOn, continuousWithinAt_iff'_left]

theorem continuous_iff'_right [TopologicalSpace Î²] {f : Î² â†’ Î±} :
    Continuous f â†” âˆ€ b, Tendsto (fun x => (f b, f x)) (ğ“ b) (ğ“¤ Î±) :=
  continuous_iff_continuousAt.trans <| forall_congr' fun _ => tendsto_nhds_right

theorem continuous_iff'_left [TopologicalSpace Î²] {f : Î² â†’ Î±} :
    Continuous f â†” âˆ€ b, Tendsto (fun x => (f x, f b)) (ğ“ b) (ğ“¤ Î±) :=
  continuous_iff_continuousAt.trans <| forall_congr' fun _ => tendsto_nhds_left

/-- Consider two functions `f` and `g` which coincide on a set `s` and are continuous there.
Then there is an open neighborhood of `s` on which `f` and `g` are uniformly close. -/
lemma exists_is_open_mem_uniformity_of_forall_mem_eq
    [TopologicalSpace Î²] {r : SetRel Î± Î±} {s : Set Î²}
    {f g : Î² â†’ Î±} (hf : âˆ€ x âˆˆ s, ContinuousAt f x) (hg : âˆ€ x âˆˆ s, ContinuousAt g x)
    (hfg : s.EqOn f g) (hr : r âˆˆ ğ“¤ Î±) :
    âˆƒ t, IsOpen t âˆ§ s âŠ† t âˆ§ âˆ€ x âˆˆ t, (f x, g x) âˆˆ r := by
  have A : âˆ€ x âˆˆ s, âˆƒ t, IsOpen t âˆ§ x âˆˆ t âˆ§ âˆ€ z âˆˆ t, (f z, g z) âˆˆ r := by
    intro x hx
    obtain âŸ¨t, ht, htsymm, htrâŸ© := comp_symm_mem_uniformity_sets hr
    have A : {z | (f x, f z) âˆˆ t} âˆˆ ğ“ x := (hf x hx).preimage_mem_nhds (mem_nhds_left (f x) ht)
    have B : {z | (g x, g z) âˆˆ t} âˆˆ ğ“ x := (hg x hx).preimage_mem_nhds (mem_nhds_left (g x) ht)
    rcases _root_.mem_nhds_iff.1 (inter_mem A B) with âŸ¨u, hu, u_open, xuâŸ©
    refine âŸ¨u, u_open, xu, fun y hy â†¦ ?_âŸ©
    have I1 : (f y, f x) âˆˆ t := SetRel.symm t (hu hy).1
    have I2 : (g x, g y) âˆˆ t := (hu hy).2
    rw [hfg hx] at I1
    exact htr (SetRel.prodMk_mem_comp I1 I2)
  choose! t t_open xt ht using A
  refine âŸ¨â‹ƒ x âˆˆ s, t x, isOpen_biUnion t_open, fun x hx â†¦ mem_biUnion hx (xt x hx), ?_âŸ©
  rintro x hx
  simp only [mem_iUnion, exists_prop] at hx
  rcases hx with âŸ¨y, ys, hyâŸ©
  exact ht y ys x hy

end Uniform

theorem Filter.Tendsto.congr_uniformity {Î± Î²} [UniformSpace Î²] {f g : Î± â†’ Î²} {l : Filter Î±} {b : Î²}
    (hf : Tendsto f l (ğ“ b)) (hg : Tendsto (fun x => (f x, g x)) l (ğ“¤ Î²)) : Tendsto g l (ğ“ b) :=
  Uniform.tendsto_nhds_right.2 <| (Uniform.tendsto_nhds_right.1 hf).uniformity_trans hg

theorem Uniform.tendsto_congr {Î± Î²} [UniformSpace Î²] {f g : Î± â†’ Î²} {l : Filter Î±} {b : Î²}
    (hfg : Tendsto (fun x => (f x, g x)) l (ğ“¤ Î²)) : Tendsto f l (ğ“ b) â†” Tendsto g l (ğ“ b) :=
  âŸ¨fun h => h.congr_uniformity hfg, fun h => h.congr_uniformity hfg.uniformity_symmâŸ©
