/-
Copyright (c) 2019 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
import Mathlib.Algebra.Order.AbsoluteValue
import Mathlib.Algebra.Order.Field.Basic
import Mathlib.Analysis.Normed.Field.Basic
import Mathlib.Topology.Instances.Real
import Mathlib.Topology.UniformSpace.OfFun

/-!
# Uniform structure induced by an absolute value

We build a uniform space structure on a commutative ring `R` equipped with an absolute value into
a linear ordered field `ğ•œ`. Of course in the case `R` is `â„š`, `â„` or `â„‚` and
`ğ•œ = â„`, we get the same thing as the metric space construction, and the general construction
follows exactly the same path.

## References

* [N. Bourbaki, *Topologie gÃ©nÃ©rale*][bourbaki1966]

## Tags

absolute value, uniform spaces
-/
noncomputable section

open Set Function Filter Uniformity

namespace AbsoluteValue

variable {ğ•œ : Type*} [LinearOrderedField ğ•œ]
variable {R : Type*} [CommRing R] (abv : AbsoluteValue R ğ•œ)

/-- The uniform structure coming from an absolute value. -/
def uniformSpace : UniformSpace R :=
  .ofFun (fun x y => abv (y - x)) (by simp) (fun x y => abv.map_sub y x)
    (fun x y z => (abv.sub_le _ _ _).trans_eq (add_comm _ _))
    fun Îµ Îµ0 => âŸ¨Îµ / 2, half_pos Îµ0, fun _ hâ‚ _ hâ‚‚ => (add_lt_add hâ‚ hâ‚‚).trans_eq (add_halves Îµ)âŸ©

theorem hasBasis_uniformity :
    ğ“¤[abv.uniformSpace].HasBasis ((0 : ğ•œ) < Â·) fun Îµ => { p : R Ã— R | abv (p.2 - p.1) < Îµ } :=
  UniformSpace.hasBasis_ofFun (exists_gt _) _ _ _ _ _

/-- A real absolute value on a ring determines a `NormedRing` structure. -/
def toNormedRing {R : Type*} [Ring R] (v : AbsoluteValue R â„) : NormedRing R where
  norm := v
  dist_eq _ _ := rfl
  dist_self x := by simp only [sub_self, MulHom.toFun_eq_coe, AbsoluteValue.coe_toMulHom, map_zero]
  dist_comm := v.map_sub
  dist_triangle := v.sub_le
  edist_dist x y := rfl
  norm_mul x y := (v.map_mul x y).le
  eq_of_dist_eq_zero := by simp only [MulHom.toFun_eq_coe, AbsoluteValue.coe_toMulHom,
    AbsoluteValue.map_sub_eq_zero_iff, imp_self, implies_true]

/-- A real absolute value on a field determines a `NormedField` structure. -/
def toNormedField {K : Type*} [Field K] (v : AbsoluteValue K â„) : NormedField K where
  toField := inferInstanceAs (Field K)
  __ := v.toNormedRing
  norm_mul' := v.map_mul

end AbsoluteValue
