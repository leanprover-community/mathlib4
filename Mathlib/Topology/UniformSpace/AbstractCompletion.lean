/-
Copyright (c) 2019 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
import Mathlib.Topology.UniformSpace.UniformEmbedding
import Mathlib.Topology.UniformSpace.Equiv

#align_import topology.uniform_space.abstract_completion from "leanprover-community/mathlib"@"dc6c365e751e34d100e80fe6e314c3c3e0fd2988"

/-!
# Abstract theory of Hausdorff completions of uniform spaces

This file characterizes Hausdorff completions of a uniform space Î± as complete Hausdorff spaces
equipped with a map from Î± which has dense image and induce the original uniform structure on Î±.
Assuming these properties we "extend" uniformly continuous maps from Î± to complete Hausdorff spaces
to the completions of Î±. This is the universal property expected from a completion.
It is then used to extend uniformly continuous maps from Î± to Î±' to maps between
completions of Î± and Î±'.

This file does not construct any such completion, it only study consequences of their existence.
The first advantage is that formal properties are clearly highlighted without interference from
construction details. The second advantage is that this framework can then be used to compare
different completion constructions. See `Topology/UniformSpace/CompareReals` for an example.
Of course the comparison comes from the universal property as usual.

A general explicit construction of completions is done in `UniformSpace/Completion`, leading
to a functor from uniform spaces to complete Hausdorff uniform spaces that is left adjoint to the
inclusion, see `UniformSpace/UniformSpaceCat` for the category packaging.

## Implementation notes

A tiny technical advantage of using a characteristic predicate such as the properties listed in
`AbstractCompletion` instead of stating the universal property is that the universal property
derived from the predicate is more universe polymorphic.

## References

We don't know any traditional text discussing this. Real world mathematics simply silently
identify the results of any two constructions that lead to something one could reasonably
call a completion.

## Tags

uniform spaces, completion, universal property
-/


noncomputable section

attribute [local instance] Classical.propDecidable

open Filter Set Function

universe u

/-- A completion of `Î±` is the data of a complete separated uniform space (from the same universe)
and a map from `Î±` with dense range and inducing the original uniform structure on `Î±`. -/
structure AbstractCompletion (Î± : Type u) [UniformSpace Î±] where
  /-- The underlying space of the completion. -/
  space : Type u
  /-- A map from a space to its completion. -/
  coe : Î± â†’ space
  /-- The completion carries a uniform structure. -/
  uniformStruct : UniformSpace space
  /-- The completion is complete. -/
  complete : CompleteSpace space
  /-- The completion is a separated space. -/
  separation : SeparatedSpace space
  /-- The map into the completion is uniform-inducing. -/
  uniformInducing : UniformInducing coe
  /-- The map into the completion has dense range. -/
  dense : DenseRange coe
#align abstract_completion AbstractCompletion

attribute [local instance]
  AbstractCompletion.uniformStruct AbstractCompletion.complete AbstractCompletion.separation

namespace AbstractCompletion

variable {Î± : Type*} [UniformSpace Î±] (pkg : AbstractCompletion Î±)

local notation "hatÎ±" => pkg.space

local notation "Î¹" => pkg.coe

/-- If `Î±` is complete, then it is an abstract completion of itself. -/
def ofComplete [SeparatedSpace Î±] [CompleteSpace Î±] : AbstractCompletion Î± :=
  mk Î± id inferInstance inferInstance inferInstance uniformInducing_id denseRange_id
#align abstract_completion.of_complete AbstractCompletion.ofComplete

theorem closure_range : closure (range Î¹) = univ :=
  pkg.dense.closure_range
#align abstract_completion.closure_range AbstractCompletion.closure_range

theorem denseInducing : DenseInducing Î¹ :=
  âŸ¨pkg.uniformInducing.inducing, pkg.denseâŸ©
#align abstract_completion.dense_inducing AbstractCompletion.denseInducing

theorem uniformContinuous_coe : UniformContinuous Î¹ :=
  UniformInducing.uniformContinuous pkg.uniformInducing
#align abstract_completion.uniform_continuous_coe AbstractCompletion.uniformContinuous_coe

theorem continuous_coe : Continuous Î¹ :=
  pkg.uniformContinuous_coe.continuous
#align abstract_completion.continuous_coe AbstractCompletion.continuous_coe

@[elab_as_elim]
theorem induction_on {p : hatÎ± â†’ Prop} (a : hatÎ±) (hp : IsClosed { a | p a }) (ih : âˆ€ a, p (Î¹ a)) :
    p a :=
  isClosed_property pkg.dense hp ih a
#align abstract_completion.induction_on AbstractCompletion.induction_on

variable {Î² : Type*}

protected theorem funext [TopologicalSpace Î²] [T2Space Î²] {f g : hatÎ± â†’ Î²} (hf : Continuous f)
    (hg : Continuous g) (h : âˆ€ a, f (Î¹ a) = g (Î¹ a)) : f = g :=
  funext fun a => pkg.induction_on a (isClosed_eq hf hg) h
#align abstract_completion.funext AbstractCompletion.funext

variable [UniformSpace Î²]

section Extend

/-- Extension of maps to completions -/
protected def extend (f : Î± â†’ Î²) : hatÎ± â†’ Î² :=
  if UniformContinuous f then pkg.denseInducing.extend f else fun x => f (pkg.dense.some x)
#align abstract_completion.extend AbstractCompletion.extend

variable {f : Î± â†’ Î²}

theorem extend_def (hf : UniformContinuous f) : pkg.extend f = pkg.denseInducing.extend f :=
  if_pos hf
#align abstract_completion.extend_def AbstractCompletion.extend_def

theorem extend_coe [T2Space Î²] (hf : UniformContinuous f) (a : Î±) : (pkg.extend f) (Î¹ a) = f a := by
  rw [pkg.extend_def hf]
  -- âŠ¢ DenseInducing.extend (_ : DenseInducing pkg.coe) f (coe pkg a) = f a
  exact pkg.denseInducing.extend_eq hf.continuous a
  -- ðŸŽ‰ no goals
#align abstract_completion.extend_coe AbstractCompletion.extend_coe

variable [CompleteSpace Î²]

theorem uniformContinuous_extend : UniformContinuous (pkg.extend f) := by
  by_cases hf : UniformContinuous f
  -- âŠ¢ UniformContinuous (AbstractCompletion.extend pkg f)
  Â· rw [pkg.extend_def hf]
    -- âŠ¢ UniformContinuous (DenseInducing.extend (_ : DenseInducing pkg.coe) f)
    exact uniformContinuous_uniformly_extend pkg.uniformInducing pkg.dense hf
    -- ðŸŽ‰ no goals
  Â· change UniformContinuous (ite _ _ _)
    -- âŠ¢ UniformContinuous (if UniformContinuous f then DenseInducing.extend (_ : Den â€¦
    rw [if_neg hf]
    -- âŠ¢ UniformContinuous fun x => f (DenseRange.some (_ : DenseRange pkg.coe) x)
    exact uniformContinuous_of_const fun a b => by congr 1
    -- ðŸŽ‰ no goals
#align abstract_completion.uniform_continuous_extend AbstractCompletion.uniformContinuous_extend

theorem continuous_extend : Continuous (pkg.extend f) :=
  pkg.uniformContinuous_extend.continuous
#align abstract_completion.continuous_extend AbstractCompletion.continuous_extend

variable [SeparatedSpace Î²]

theorem extend_unique (hf : UniformContinuous f) {g : hatÎ± â†’ Î²} (hg : UniformContinuous g)
    (h : âˆ€ a : Î±, f a = g (Î¹ a)) : pkg.extend f = g := by
  apply pkg.funext pkg.continuous_extend hg.continuous
  -- âŠ¢ âˆ€ (a : Î±), AbstractCompletion.extend pkg f (coe pkg a) = g (coe pkg a)
  simpa only [pkg.extend_coe hf] using h
  -- ðŸŽ‰ no goals
#align abstract_completion.extend_unique AbstractCompletion.extend_unique

@[simp]
theorem extend_comp_coe {f : hatÎ± â†’ Î²} (hf : UniformContinuous f) : pkg.extend (f âˆ˜ Î¹) = f :=
  funext fun x =>
    pkg.induction_on x (isClosed_eq pkg.continuous_extend hf.continuous) fun y =>
      pkg.extend_coe (hf.comp <| pkg.uniformContinuous_coe) y
#align abstract_completion.extend_comp_coe AbstractCompletion.extend_comp_coe

end Extend

section MapSec

variable (pkg' : AbstractCompletion Î²)

local notation "hatÎ²" => pkg'.space

local notation "Î¹'" => pkg'.coe

/-- Lifting maps to completions -/
protected def map (f : Î± â†’ Î²) : hatÎ± â†’ hatÎ² :=
  pkg.extend (Î¹' âˆ˜ f)
#align abstract_completion.map AbstractCompletion.map

local notation "map" => pkg.map pkg'

variable (f : Î± â†’ Î²)

theorem uniformContinuous_map : UniformContinuous (map f) :=
  pkg.uniformContinuous_extend
#align abstract_completion.uniform_continuous_map AbstractCompletion.uniformContinuous_map

@[continuity]
theorem continuous_map : Continuous (map f) :=
  pkg.continuous_extend
#align abstract_completion.continuous_map AbstractCompletion.continuous_map

variable {f}

@[simp]
theorem map_coe (hf : UniformContinuous f) (a : Î±) : map f (Î¹ a) = Î¹' (f a) :=
  pkg.extend_coe (pkg'.uniformContinuous_coe.comp hf) a
#align abstract_completion.map_coe AbstractCompletion.map_coe

theorem map_unique {f : Î± â†’ Î²} {g : hatÎ± â†’ hatÎ²} (hg : UniformContinuous g)
    (h : âˆ€ a, Î¹' (f a) = g (Î¹ a)) : map f = g :=
  pkg.funext (pkg.continuous_map _ _) hg.continuous <| by
    intro a
    -- âŠ¢ AbstractCompletion.map pkg pkg' f (coe pkg a) = g (coe pkg a)
    change pkg.extend (Î¹' âˆ˜ f) _ = _
    -- âŠ¢ AbstractCompletion.extend pkg (pkg'.coe âˆ˜ f) (coe pkg a) = g (coe pkg a)
    simp only [(Â· âˆ˜ Â·), h, â†comp_apply (f := g)]
    -- âŠ¢ AbstractCompletion.extend pkg (fun x => (g âˆ˜ pkg.coe) x) (coe pkg a) = (g âˆ˜  â€¦
    rw [pkg.extend_coe (hg.comp pkg.uniformContinuous_coe)]
    -- ðŸŽ‰ no goals
#align abstract_completion.map_unique AbstractCompletion.map_unique

@[simp]
theorem map_id : pkg.map pkg id = id :=
  pkg.map_unique pkg uniformContinuous_id fun _ => rfl
#align abstract_completion.map_id AbstractCompletion.map_id

variable {Î³ : Type*} [UniformSpace Î³]

theorem extend_map [CompleteSpace Î³] [SeparatedSpace Î³] {f : Î² â†’ Î³} {g : Î± â†’ Î²}
    (hf : UniformContinuous f) (hg : UniformContinuous g) :
    pkg'.extend f âˆ˜ map g = pkg.extend (f âˆ˜ g) :=
  pkg.funext (pkg'.continuous_extend.comp (pkg.continuous_map pkg' _)) pkg.continuous_extend
    fun a => by
    rw [pkg.extend_coe (hf.comp hg), comp_apply, pkg.map_coe pkg' hg, pkg'.extend_coe hf]
    -- âŠ¢ f (g a) = (f âˆ˜ g) a
    rfl
    -- ðŸŽ‰ no goals
#align abstract_completion.extend_map AbstractCompletion.extend_map

variable (pkg'' : AbstractCompletion Î³)

theorem map_comp {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : UniformContinuous g) (hf : UniformContinuous f) :
    pkg'.map pkg'' g âˆ˜ pkg.map pkg' f = pkg.map pkg'' (g âˆ˜ f) :=
  pkg.extend_map pkg' (pkg''.uniformContinuous_coe.comp hg) hf
#align abstract_completion.map_comp AbstractCompletion.map_comp

end MapSec

section Compare

-- We can now compare two completion packages for the same uniform space
variable (pkg' : AbstractCompletion Î±)

/-- The comparison map between two completions of the same uniform space. -/
def compare : pkg.space â†’ pkg'.space :=
  pkg.extend pkg'.coe
#align abstract_completion.compare AbstractCompletion.compare

theorem uniformContinuous_compare : UniformContinuous (pkg.compare pkg') :=
  pkg.uniformContinuous_extend
#align abstract_completion.uniform_continuous_compare AbstractCompletion.uniformContinuous_compare

theorem compare_coe (a : Î±) : pkg.compare pkg' (pkg.coe a) = pkg'.coe a :=
  pkg.extend_coe pkg'.uniformContinuous_coe a
#align abstract_completion.compare_coe AbstractCompletion.compare_coe

theorem inverse_compare : pkg.compare pkg' âˆ˜ pkg'.compare pkg = id := by
  have uc := pkg.uniformContinuous_compare pkg'
  -- âŠ¢ compare pkg pkg' âˆ˜ compare pkg' pkg = id
  have uc' := pkg'.uniformContinuous_compare pkg
  -- âŠ¢ compare pkg pkg' âˆ˜ compare pkg' pkg = id
  apply pkg'.funext (uc.comp uc').continuous continuous_id
  -- âŠ¢ âˆ€ (a : Î±), (compare pkg pkg' âˆ˜ compare pkg' pkg) (coe pkg' a) = id (coe pkg' â€¦
  intro a
  -- âŠ¢ (compare pkg pkg' âˆ˜ compare pkg' pkg) (coe pkg' a) = id (coe pkg' a)
  rw [comp_apply, pkg'.compare_coe pkg, pkg.compare_coe pkg']
  -- âŠ¢ coe pkg' a = id (coe pkg' a)
  rfl
  -- ðŸŽ‰ no goals
#align abstract_completion.inverse_compare AbstractCompletion.inverse_compare

/-- The uniform bijection between two completions of the same uniform space. -/
def compareEquiv : pkg.space â‰ƒáµ¤ pkg'.space
    where
  toFun := pkg.compare pkg'
  invFun := pkg'.compare pkg
  left_inv := congr_fun (pkg'.inverse_compare pkg)
  right_inv := congr_fun (pkg.inverse_compare pkg')
  uniformContinuous_toFun := uniformContinuous_compare _ _
  uniformContinuous_invFun := uniformContinuous_compare _ _
#align abstract_completion.compare_equiv AbstractCompletion.compareEquiv

theorem uniformContinuous_compareEquiv : UniformContinuous (pkg.compareEquiv pkg') :=
  pkg.uniformContinuous_compare pkg'
#align abstract_completion.uniform_continuous_compare_equiv AbstractCompletion.uniformContinuous_compareEquiv

theorem uniformContinuous_compareEquiv_symm : UniformContinuous (pkg.compareEquiv pkg').symm :=
  pkg'.uniformContinuous_compare pkg
#align abstract_completion.uniform_continuous_compare_equiv_symm AbstractCompletion.uniformContinuous_compareEquiv_symm

end Compare

section Prod

variable (pkg' : AbstractCompletion Î²)

local notation "hatÎ²" => pkg'.space

local notation "Î¹'" => pkg'.coe

/-- Products of completions -/
protected def prod : AbstractCompletion (Î± Ã— Î²)
    where
  space := hatÎ± Ã— hatÎ²
  coe p := âŸ¨Î¹ p.1, Î¹' p.2âŸ©
  uniformStruct := inferInstance
  complete := inferInstance
  separation := inferInstance
  uniformInducing := UniformInducing.prod pkg.uniformInducing pkg'.uniformInducing
  dense := DenseRange.prod_map pkg.dense pkg'.dense
#align abstract_completion.prod AbstractCompletion.prod

end Prod

section Extensionâ‚‚

variable (pkg' : AbstractCompletion Î²)

local notation "hatÎ²" => pkg'.space

local notation "Î¹'" => pkg'.coe

variable {Î³ : Type*} [UniformSpace Î³]

open Function

/-- Extend two variable map to completions. -/
protected def extendâ‚‚ (f : Î± â†’ Î² â†’ Î³) : hatÎ± â†’ hatÎ² â†’ Î³ :=
  curry <| (pkg.prod pkg').extend (uncurry f)
#align abstract_completion.extendâ‚‚ AbstractCompletion.extendâ‚‚

section SeparatedSpace

variable [SeparatedSpace Î³] {f : Î± â†’ Î² â†’ Î³}

theorem extensionâ‚‚_coe_coe (hf : UniformContinuous <| uncurry f) (a : Î±) (b : Î²) :
    pkg.extendâ‚‚ pkg' f (Î¹ a) (Î¹' b) = f a b :=
  show (pkg.prod pkg').extend (uncurry f) ((pkg.prod pkg').coe (a, b)) = uncurry f (a, b) from
    (pkg.prod pkg').extend_coe hf _
#align abstract_completion.extensionâ‚‚_coe_coe AbstractCompletion.extensionâ‚‚_coe_coe

end SeparatedSpace

variable {f : Î± â†’ Î² â†’ Î³}

variable [CompleteSpace Î³] (f)

theorem uniformContinuous_extensionâ‚‚ : UniformContinuousâ‚‚ (pkg.extendâ‚‚ pkg' f) := by
  rw [uniformContinuousâ‚‚_def, AbstractCompletion.extendâ‚‚, uncurry_curry]
  -- âŠ¢ UniformContinuous (AbstractCompletion.extend (AbstractCompletion.prod pkg pk â€¦
  apply uniformContinuous_extend
  -- ðŸŽ‰ no goals
#align abstract_completion.uniform_continuous_extensionâ‚‚ AbstractCompletion.uniformContinuous_extensionâ‚‚

end Extensionâ‚‚

section Mapâ‚‚

variable (pkg' : AbstractCompletion Î²)

local notation "hatÎ²" => pkg'.space

local notation "Î¹'" => pkg'.coe

variable {Î³ : Type*} [UniformSpace Î³] (pkg'' : AbstractCompletion Î³)

local notation "hatÎ³" => pkg''.space

local notation "Î¹''" => pkg''.coe

local notation f " âˆ˜â‚‚ " g => bicompr f g

/-- Lift two variable maps to completions. -/
protected def mapâ‚‚ (f : Î± â†’ Î² â†’ Î³) : hatÎ± â†’ hatÎ² â†’ hatÎ³ :=
  pkg.extendâ‚‚ pkg' (pkg''.coe âˆ˜â‚‚ f)
#align abstract_completion.mapâ‚‚ AbstractCompletion.mapâ‚‚

theorem uniformContinuous_mapâ‚‚ (f : Î± â†’ Î² â†’ Î³) : UniformContinuousâ‚‚ (pkg.mapâ‚‚ pkg' pkg'' f) :=
  AbstractCompletion.uniformContinuous_extensionâ‚‚ pkg pkg' _
#align abstract_completion.uniform_continuous_mapâ‚‚ AbstractCompletion.uniformContinuous_mapâ‚‚

theorem continuous_mapâ‚‚ {Î´} [TopologicalSpace Î´] {f : Î± â†’ Î² â†’ Î³} {a : Î´ â†’ hatÎ±} {b : Î´ â†’ hatÎ²}
    (ha : Continuous a) (hb : Continuous b) :
    Continuous fun d : Î´ => pkg.mapâ‚‚ pkg' pkg'' f (a d) (b d) :=
  ((pkg.uniformContinuous_mapâ‚‚ pkg' pkg'' f).continuous.comp (Continuous.prod_mk ha hb) : _)
#align abstract_completion.continuous_mapâ‚‚ AbstractCompletion.continuous_mapâ‚‚

theorem mapâ‚‚_coe_coe (a : Î±) (b : Î²) (f : Î± â†’ Î² â†’ Î³) (hf : UniformContinuousâ‚‚ f) :
    pkg.mapâ‚‚ pkg' pkg'' f (Î¹ a) (Î¹' b) = Î¹'' (f a b) :=
  pkg.extensionâ‚‚_coe_coe (f := pkg''.coe âˆ˜â‚‚ f) pkg' (pkg''.uniformContinuous_coe.comp hf) a b
#align abstract_completion.mapâ‚‚_coe_coe AbstractCompletion.mapâ‚‚_coe_coe

end Mapâ‚‚

end AbstractCompletion
