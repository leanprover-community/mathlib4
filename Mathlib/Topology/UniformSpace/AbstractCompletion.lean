/-
Copyright (c) 2019 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
import Mathlib.Topology.UniformSpace.UniformEmbedding
import Mathlib.Topology.UniformSpace.Equiv

/-!
# Abstract theory of Hausdorff completions of uniform spaces

This file characterizes Hausdorff completions of a uniform space Œ± as complete Hausdorff spaces
equipped with a map from Œ± which has dense image and induce the original uniform structure on Œ±.
Assuming these properties we "extend" uniformly continuous maps from Œ± to complete Hausdorff spaces
to the completions of Œ±. This is the universal property expected from a completion.
It is then used to extend uniformly continuous maps from Œ± to Œ±' to maps between
completions of Œ± and Œ±'.

This file does not construct any such completion, it only study consequences of their existence.
The first advantage is that formal properties are clearly highlighted without interference from
construction details. The second advantage is that this framework can then be used to compare
different completion constructions. See `Topology/UniformSpace/CompareReals` for an example.
Of course the comparison comes from the universal property as usual.

A general explicit construction of completions is done in `UniformSpace/Completion`, leading
to a functor from uniform spaces to complete Hausdorff uniform spaces that is left adjoint to the
inclusion, see `UniformSpace/UniformSpaceCat` for the category packaging.

## Implementation notes

A tiny technical advantage of using a characteristic predicate such as the properties listed in
`AbstractCompletion` instead of stating the universal property is that the universal property
derived from the predicate is more universe polymorphic.

## References

We don't know any traditional text discussing this. Real world mathematics simply silently
identify the results of any two constructions that lead to something one could reasonably
call a completion.

## Tags

uniform spaces, completion, universal property
-/


noncomputable section

attribute [local instance] Classical.propDecidable

open Filter Set Function

universe u

/-- A completion of `Œ±` is the data of a complete separated uniform space (from the same universe)
and a map from `Œ±` with dense range and inducing the original uniform structure on `Œ±`. -/
structure AbstractCompletion (Œ± : Type u) [UniformSpace Œ±] where
  /-- The underlying space of the completion. -/
  space : Type u
  /-- A map from a space to its completion. -/
  coe : Œ± ‚Üí space
  /-- The completion carries a uniform structure. -/
  uniformStruct : UniformSpace space
  /-- The completion is complete. -/
  complete : CompleteSpace space
  /-- The completion is a T‚ÇÄ space. -/
  separation : T0Space space
  /-- The map into the completion is uniform-inducing. -/
  uniformInducing : UniformInducing coe
  /-- The map into the completion has dense range. -/
  dense : DenseRange coe

attribute [local instance]
  AbstractCompletion.uniformStruct AbstractCompletion.complete AbstractCompletion.separation

namespace AbstractCompletion

variable {Œ± : Type*} [UniformSpace Œ±] (pkg : AbstractCompletion Œ±)

local notation "hatŒ±" => pkg.space

local notation "Œπ" => pkg.coe

/-- If `Œ±` is complete, then it is an abstract completion of itself. -/
def ofComplete [T0Space Œ±] [CompleteSpace Œ±] : AbstractCompletion Œ± :=
  mk Œ± id inferInstance inferInstance inferInstance uniformInducing_id denseRange_id

theorem closure_range : closure (range Œπ) = univ :=
  pkg.dense.closure_range

theorem denseInducing : DenseInducing Œπ :=
  ‚ü®pkg.uniformInducing.inducing, pkg.dense‚ü©

theorem uniformContinuous_coe : UniformContinuous Œπ :=
  UniformInducing.uniformContinuous pkg.uniformInducing

theorem continuous_coe : Continuous Œπ :=
  pkg.uniformContinuous_coe.continuous

@[elab_as_elim]
theorem induction_on {p : hatŒ± ‚Üí Prop} (a : hatŒ±) (hp : IsClosed { a | p a }) (ih : ‚àÄ a, p (Œπ a)) :
    p a :=
  isClosed_property pkg.dense hp ih a

variable {Œ≤ : Type*}

protected theorem funext [TopologicalSpace Œ≤] [T2Space Œ≤] {f g : hatŒ± ‚Üí Œ≤} (hf : Continuous f)
    (hg : Continuous g) (h : ‚àÄ a, f (Œπ a) = g (Œπ a)) : f = g :=
  funext fun a => pkg.induction_on a (isClosed_eq hf hg) h

variable [UniformSpace Œ≤]

section Extend

/-- Extension of maps to completions -/
protected def extend (f : Œ± ‚Üí Œ≤) : hatŒ± ‚Üí Œ≤ :=
  if UniformContinuous f then pkg.denseInducing.extend f else fun x => f (pkg.dense.some x)

variable {f : Œ± ‚Üí Œ≤}

theorem extend_def (hf : UniformContinuous f) : pkg.extend f = pkg.denseInducing.extend f :=
  if_pos hf

theorem extend_coe [T2Space Œ≤] (hf : UniformContinuous f) (a : Œ±) : (pkg.extend f) (Œπ a) = f a := by
  rw [pkg.extend_def hf]
  exact pkg.denseInducing.extend_eq hf.continuous a

variable [CompleteSpace Œ≤]

theorem uniformContinuous_extend : UniformContinuous (pkg.extend f) := by
  by_cases hf : UniformContinuous f
  ¬∑ rw [pkg.extend_def hf]
    exact uniformContinuous_uniformly_extend pkg.uniformInducing pkg.dense hf
  ¬∑ change UniformContinuous (ite _ _ _)
    rw [if_neg hf]
    exact uniformContinuous_of_const fun a b => by congr 1

theorem continuous_extend : Continuous (pkg.extend f) :=
  pkg.uniformContinuous_extend.continuous

variable [T0Space Œ≤]

theorem extend_unique (hf : UniformContinuous f) {g : hatŒ± ‚Üí Œ≤} (hg : UniformContinuous g)
    (h : ‚àÄ a : Œ±, f a = g (Œπ a)) : pkg.extend f = g := by
  apply pkg.funext pkg.continuous_extend hg.continuous
  simpa only [pkg.extend_coe hf] using h

@[simp]
theorem extend_comp_coe {f : hatŒ± ‚Üí Œ≤} (hf : UniformContinuous f) : pkg.extend (f ‚àò Œπ) = f :=
  funext fun x =>
    pkg.induction_on x (isClosed_eq pkg.continuous_extend hf.continuous) fun y =>
      pkg.extend_coe (hf.comp <| pkg.uniformContinuous_coe) y

end Extend

section MapSec

variable (pkg' : AbstractCompletion Œ≤)

local notation "hatŒ≤" => pkg'.space

local notation "Œπ'" => pkg'.coe

/-- Lifting maps to completions -/
protected def map (f : Œ± ‚Üí Œ≤) : hatŒ± ‚Üí hatŒ≤ :=
  pkg.extend (Œπ' ‚àò f)

local notation "map" => pkg.map pkg'

variable (f : Œ± ‚Üí Œ≤)

theorem uniformContinuous_map : UniformContinuous (map f) :=
  pkg.uniformContinuous_extend

@[continuity]
theorem continuous_map : Continuous (map f) :=
  pkg.continuous_extend

variable {f}

@[simp]
theorem map_coe (hf : UniformContinuous f) (a : Œ±) : map f (Œπ a) = Œπ' (f a) :=
  pkg.extend_coe (pkg'.uniformContinuous_coe.comp hf) a

theorem map_unique {f : Œ± ‚Üí Œ≤} {g : hatŒ± ‚Üí hatŒ≤} (hg : UniformContinuous g)
    (h : ‚àÄ a, Œπ' (f a) = g (Œπ a)) : map f = g :=
  pkg.funext (pkg.continuous_map _ _) hg.continuous <| by
    intro a
    change pkg.extend (Œπ' ‚àò f) _ = _
    simp_rw [Function.comp_def, h, ‚Üê comp_apply (f := g)]
    rw [pkg.extend_coe (hg.comp pkg.uniformContinuous_coe)]

@[simp]
theorem map_id : pkg.map pkg id = id :=
  pkg.map_unique pkg uniformContinuous_id fun _ => rfl

variable {Œ≥ : Type*} [UniformSpace Œ≥]

theorem extend_map [CompleteSpace Œ≥] [T0Space Œ≥] {f : Œ≤ ‚Üí Œ≥} {g : Œ± ‚Üí Œ≤}
    (hf : UniformContinuous f) (hg : UniformContinuous g) :
    pkg'.extend f ‚àò map g = pkg.extend (f ‚àò g) :=
  pkg.funext (pkg'.continuous_extend.comp (pkg.continuous_map pkg' _)) pkg.continuous_extend
    fun a => by
    rw [pkg.extend_coe (hf.comp hg), comp_apply, pkg.map_coe pkg' hg, pkg'.extend_coe hf]
    rfl

variable (pkg'' : AbstractCompletion Œ≥)

theorem map_comp {g : Œ≤ ‚Üí Œ≥} {f : Œ± ‚Üí Œ≤} (hg : UniformContinuous g) (hf : UniformContinuous f) :
    pkg'.map pkg'' g ‚àò pkg.map pkg' f = pkg.map pkg'' (g ‚àò f) :=
  pkg.extend_map pkg' (pkg''.uniformContinuous_coe.comp hg) hf

end MapSec

section Compare

-- We can now compare two completion packages for the same uniform space
variable (pkg' : AbstractCompletion Œ±)

/-- The comparison map between two completions of the same uniform space. -/
def compare : pkg.space ‚Üí pkg'.space :=
  pkg.extend pkg'.coe

theorem uniformContinuous_compare : UniformContinuous (pkg.compare pkg') :=
  pkg.uniformContinuous_extend

theorem compare_coe (a : Œ±) : pkg.compare pkg' (pkg.coe a) = pkg'.coe a :=
  pkg.extend_coe pkg'.uniformContinuous_coe a

theorem inverse_compare : pkg.compare pkg' ‚àò pkg'.compare pkg = id := by
  have uc := pkg.uniformContinuous_compare pkg'
  have uc' := pkg'.uniformContinuous_compare pkg
  apply pkg'.funext (uc.comp uc').continuous continuous_id
  intro a
  rw [comp_apply, pkg'.compare_coe pkg, pkg.compare_coe pkg']
  rfl

/-- The uniform bijection between two completions of the same uniform space. -/
def compareEquiv : pkg.space ‚âÉ·µ§ pkg'.space where
  toFun := pkg.compare pkg'
  invFun := pkg'.compare pkg
  left_inv := congr_fun (pkg'.inverse_compare pkg)
  right_inv := congr_fun (pkg.inverse_compare pkg')
  uniformContinuous_toFun := uniformContinuous_compare _ _
  uniformContinuous_invFun := uniformContinuous_compare _ _

theorem uniformContinuous_compareEquiv : UniformContinuous (pkg.compareEquiv pkg') :=
  pkg.uniformContinuous_compare pkg'

theorem uniformContinuous_compareEquiv_symm : UniformContinuous (pkg.compareEquiv pkg').symm :=
  pkg'.uniformContinuous_compare pkg


open scoped Topology

/-Let `f : Œ± ‚Üí Œ≥` be a continuous function between a uniform space `Œ±` and a regular topological
space `Œ≥`, and let `pkg, pkg'` be two abstract completions of `Œ±`. Then
if for every point `a : pkg` the filter `f.map (coe‚Åª¬π (ùìù a))` obtained by pushing forward with `f`
the preimage in `Œ±` of `ùìù a` tends to `ùìù (f.extend a : Œ≤)`, then the comparison map
between `pkg` and `pkg'` composed with the extension of `f` to `pkg`` coincides with the
extension of `f` to `pkg'`. The situation is described in the following diagram, where the
two diagonal arrows are the extensions of `f` to the two different completions `pkg` and `pkg'`;
the statement of `compare_comp_eq_compare` is the commutativity of the right triangle.

```
`Œ±^`=`pkg` ‚âÖ `Œ±^'`=`pkg'`   *here `‚âÖ` is `compare`*
  ‚àß     \        /
  |      \      /
  |       \    /
  |        V  ‚à®
 Œ± ---f---> Œ≥
```
 -/
theorem compare_comp_eq_compare (Œ≥ : Type*) [TopologicalSpace Œ≥]
    [T3Space Œ≥] {f : Œ± ‚Üí Œ≥} (cont_f : Continuous f) :
    letI := pkg.uniformStruct.toTopologicalSpace
    letI := pkg'.uniformStruct.toTopologicalSpace
    (‚àÄ a : pkg.space,
      Filter.Tendsto f (Filter.comap pkg.coe (ùìù a)) (ùìù ((pkg.denseInducing.extend f) a))) ‚Üí
      pkg.denseInducing.extend f ‚àò pkg'.compare pkg = pkg'.denseInducing.extend f := by
  let _ := pkg'.uniformStruct
  let _ := pkg.uniformStruct
  intro h
  have (x : Œ±) : (pkg.denseInducing.extend f ‚àò pkg'.compare pkg) (pkg'.coe x) = f x := by
    simp only [Function.comp_apply, compare_coe, DenseInducing.extend_eq _ cont_f, implies_true]
  apply (DenseInducing.extend_unique (AbstractCompletion.denseInducing _) this
    (Continuous.comp _ (uniformContinuous_compare pkg' pkg).continuous )).symm
  apply DenseInducing.continuous_extend
  exact fun a ‚Ü¶ ‚ü®(pkg.denseInducing.extend f) a, h a‚ü©

end Compare

section Prod

variable (pkg' : AbstractCompletion Œ≤)

local notation "hatŒ≤" => pkg'.space

local notation "Œπ'" => pkg'.coe

/-- Products of completions -/
protected def prod : AbstractCompletion (Œ± √ó Œ≤) where
  space := hatŒ± √ó hatŒ≤
  coe p := ‚ü®Œπ p.1, Œπ' p.2‚ü©
  uniformStruct := inferInstance
  complete := inferInstance
  separation := inferInstance
  uniformInducing := UniformInducing.prod pkg.uniformInducing pkg'.uniformInducing
  dense := DenseRange.prod_map pkg.dense pkg'.dense

end Prod

section Extension‚ÇÇ

variable (pkg' : AbstractCompletion Œ≤)

local notation "hatŒ≤" => pkg'.space

local notation "Œπ'" => pkg'.coe

variable {Œ≥ : Type*} [UniformSpace Œ≥]

open Function

/-- Extend two variable map to completions. -/
protected def extend‚ÇÇ (f : Œ± ‚Üí Œ≤ ‚Üí Œ≥) : hatŒ± ‚Üí hatŒ≤ ‚Üí Œ≥ :=
  curry <| (pkg.prod pkg').extend (uncurry f)

section T0Space

variable [T0Space Œ≥] {f : Œ± ‚Üí Œ≤ ‚Üí Œ≥}

theorem extension‚ÇÇ_coe_coe (hf : UniformContinuous <| uncurry f) (a : Œ±) (b : Œ≤) :
    pkg.extend‚ÇÇ pkg' f (Œπ a) (Œπ' b) = f a b :=
  show (pkg.prod pkg').extend (uncurry f) ((pkg.prod pkg').coe (a, b)) = uncurry f (a, b) from
    (pkg.prod pkg').extend_coe hf _

end T0Space

variable {f : Œ± ‚Üí Œ≤ ‚Üí Œ≥}
variable [CompleteSpace Œ≥] (f)

theorem uniformContinuous_extension‚ÇÇ : UniformContinuous‚ÇÇ (pkg.extend‚ÇÇ pkg' f) := by
  rw [uniformContinuous‚ÇÇ_def, AbstractCompletion.extend‚ÇÇ, uncurry_curry]
  apply uniformContinuous_extend

end Extension‚ÇÇ

section Map‚ÇÇ

variable (pkg' : AbstractCompletion Œ≤)

local notation "hatŒ≤" => pkg'.space

local notation "Œπ'" => pkg'.coe

variable {Œ≥ : Type*} [UniformSpace Œ≥] (pkg'' : AbstractCompletion Œ≥)

local notation "hatŒ≥" => pkg''.space

local notation "Œπ''" => pkg''.coe

local notation f " ‚àò‚ÇÇ " g => bicompr f g

/-- Lift two variable maps to completions. -/
protected def map‚ÇÇ (f : Œ± ‚Üí Œ≤ ‚Üí Œ≥) : hatŒ± ‚Üí hatŒ≤ ‚Üí hatŒ≥ :=
  pkg.extend‚ÇÇ pkg' (pkg''.coe ‚àò‚ÇÇ f)

theorem uniformContinuous_map‚ÇÇ (f : Œ± ‚Üí Œ≤ ‚Üí Œ≥) : UniformContinuous‚ÇÇ (pkg.map‚ÇÇ pkg' pkg'' f) :=
  AbstractCompletion.uniformContinuous_extension‚ÇÇ pkg pkg' _

theorem continuous_map‚ÇÇ {Œ¥} [TopologicalSpace Œ¥] {f : Œ± ‚Üí Œ≤ ‚Üí Œ≥} {a : Œ¥ ‚Üí hatŒ±} {b : Œ¥ ‚Üí hatŒ≤}
    (ha : Continuous a) (hb : Continuous b) :
    Continuous fun d : Œ¥ => pkg.map‚ÇÇ pkg' pkg'' f (a d) (b d) :=
  ((pkg.uniformContinuous_map‚ÇÇ pkg' pkg'' f).continuous.comp (Continuous.prod_mk ha hb) : _)

theorem map‚ÇÇ_coe_coe (a : Œ±) (b : Œ≤) (f : Œ± ‚Üí Œ≤ ‚Üí Œ≥) (hf : UniformContinuous‚ÇÇ f) :
    pkg.map‚ÇÇ pkg' pkg'' f (Œπ a) (Œπ' b) = Œπ'' (f a b) :=
  pkg.extension‚ÇÇ_coe_coe (f := pkg''.coe ‚àò‚ÇÇ f) pkg' (pkg''.uniformContinuous_coe.comp hf) a b

end Map‚ÇÇ

end AbstractCompletion
