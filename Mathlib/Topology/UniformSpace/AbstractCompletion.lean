/-
Copyright (c) 2019 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
import Mathlib.Topology.UniformSpace.UniformEmbedding
import Mathlib.Topology.UniformSpace.Equiv

/-!
# Abstract theory of Hausdorff completions of uniform spaces

This file characterizes Hausdorff completions of a uniform space Î± as complete Hausdorff spaces
equipped with a map from Î± which has dense image and induce the original uniform structure on Î±.
Assuming these properties we "extend" uniformly continuous maps from Î± to complete Hausdorff spaces
to the completions of Î±. This is the universal property expected from a completion.
It is then used to extend uniformly continuous maps from Î± to Î±' to maps between
completions of Î± and Î±'.

This file does not construct any such completion, it only study consequences of their existence.
The first advantage is that formal properties are clearly highlighted without interference from
construction details. The second advantage is that this framework can then be used to compare
different completion constructions. See `Topology/UniformSpace/CompareReals` for an example.
Of course the comparison comes from the universal property as usual.

A general explicit construction of completions is done in `UniformSpace/Completion`, leading
to a functor from uniform spaces to complete Hausdorff uniform spaces that is left adjoint to the
inclusion, see `UniformSpace/UniformSpaceCat` for the category packaging.

## Implementation notes

A tiny technical advantage of using a characteristic predicate such as the properties listed in
`AbstractCompletion` instead of stating the universal property is that the universal property
derived from the predicate is more universe polymorphic.

## References

We don't know any traditional text discussing this. Real world mathematics simply silently
identify the results of any two constructions that lead to something one could reasonably
call a completion.

## Tags

uniform spaces, completion, universal property
-/


noncomputable section

open Filter Set Function

/-- A completion of `Î±` is the data of a complete separated uniform space
and a map from `Î±` with dense range and inducing the original uniform structure on `Î±`. -/
@[pp_with_univ]
structure AbstractCompletion.{v, u} (Î± : Type u) [UniformSpace Î±] where
  /-- The underlying space of the completion. -/
  space : Type v
  /-- A map from a space to its completion. -/
  coe : Î± â†’ space
  /-- The completion carries a uniform structure. -/
  uniformStruct : UniformSpace space
  /-- The completion is complete. -/
  complete : CompleteSpace space
  /-- The completion is a Tâ‚€ space. -/
  separation : T0Space space
  /-- The map into the completion is uniform-inducing. -/
  isUniformInducing : IsUniformInducing coe
  /-- The map into the completion has dense range. -/
  dense : DenseRange coe

attribute [local instance]
  AbstractCompletion.uniformStruct AbstractCompletion.complete AbstractCompletion.separation

namespace AbstractCompletion

universe uÎ± vÎ± vÎ±' uÎ² vÎ² uÎ³ vÎ³

variable {Î± : Type uÎ±} [UniformSpace Î±] (pkg : AbstractCompletion.{vÎ±} Î±)

local notation "hatÎ±" => pkg.space

local notation "Î¹" => pkg.coe

/-- If `Î±` is complete, then it is an abstract completion of itself. -/
def ofComplete [T0Space Î±] [CompleteSpace Î±] : AbstractCompletion Î± :=
  mk Î± id inferInstance inferInstance inferInstance .id denseRange_id

theorem closure_range : closure (range Î¹) = univ :=
  pkg.dense.closure_range

theorem isDenseInducing : IsDenseInducing Î¹ :=
  âŸ¨pkg.isUniformInducing.isInducing, pkg.denseâŸ©

theorem uniformContinuous_coe : UniformContinuous Î¹ :=
  IsUniformInducing.uniformContinuous pkg.isUniformInducing

theorem continuous_coe : Continuous Î¹ :=
  pkg.uniformContinuous_coe.continuous

@[elab_as_elim]
theorem induction_on {p : hatÎ± â†’ Prop} (a : hatÎ±) (hp : IsClosed { a | p a }) (ih : âˆ€ a, p (Î¹ a)) :
    p a :=
  isClosed_property pkg.dense hp ih a

variable {Î² : Type uÎ²}

protected theorem funext [TopologicalSpace Î²] [T2Space Î²] {f g : hatÎ± â†’ Î²} (hf : Continuous f)
    (hg : Continuous g) (h : âˆ€ a, f (Î¹ a) = g (Î¹ a)) : f = g :=
  funext fun a => pkg.induction_on a (isClosed_eq hf hg) h

variable [UniformSpace Î²]

section Extend

/-- Extension of maps to completions -/
protected def extend (f : Î± â†’ Î²) : hatÎ± â†’ Î² :=
  open scoped Classical in
  if UniformContinuous f then pkg.isDenseInducing.extend f else fun x => f (pkg.dense.some x)

variable {f : Î± â†’ Î²}

theorem extend_def (hf : UniformContinuous f) : pkg.extend f = pkg.isDenseInducing.extend f :=
  if_pos hf

theorem inseparable_extend_coe (hf : UniformContinuous f) (x : Î±) :
    Inseparable (pkg.extend f (Î¹ x)) (f x) := by
  rw [extend_def _ hf]
  exact pkg.isDenseInducing.inseparable_extend hf.continuous.continuousAt

theorem extend_coe [T2Space Î²] (hf : UniformContinuous f) (a : Î±) : (pkg.extend f) (Î¹ a) = f a := by
  rw [pkg.extend_def hf]
  exact pkg.isDenseInducing.extend_eq hf.continuous a

variable [CompleteSpace Î²]

theorem uniformContinuous_extend : UniformContinuous (pkg.extend f) := by
  by_cases hf : UniformContinuous f
  Â· rw [pkg.extend_def hf]
    exact uniformContinuous_uniformly_extend pkg.isUniformInducing pkg.dense hf
  Â· unfold AbstractCompletion.extend
    rw [if_neg hf]
    exact uniformContinuous_of_const fun a b => by congr 1

theorem continuous_extend : Continuous (pkg.extend f) :=
  pkg.uniformContinuous_extend.continuous

lemma isUniformInducing_extend (h : IsUniformInducing f) :
    IsUniformInducing (pkg.extend f) := by
  rw [extend_def _ h.uniformContinuous]
  exact pkg.isDenseInducing.isUniformInducing_extend pkg.isUniformInducing h

variable [T0Space Î²]

theorem extend_unique (hf : UniformContinuous f) {g : hatÎ± â†’ Î²} (hg : UniformContinuous g)
    (h : âˆ€ a : Î±, f a = g (Î¹ a)) : pkg.extend f = g := by
  apply pkg.funext pkg.continuous_extend hg.continuous
  simpa only [pkg.extend_coe hf] using h

@[simp]
theorem extend_comp_coe {f : hatÎ± â†’ Î²} (hf : UniformContinuous f) : pkg.extend (f âˆ˜ Î¹) = f :=
  funext fun x =>
    pkg.induction_on x (isClosed_eq pkg.continuous_extend hf.continuous) fun y =>
      pkg.extend_coe (hf.comp <| pkg.uniformContinuous_coe) y

end Extend

section MapSec

variable (pkg' : AbstractCompletion.{vÎ²} Î²)

local notation "hatÎ²" => pkg'.space

local notation "Î¹'" => pkg'.coe

/-- Lifting maps to completions -/
protected def map (f : Î± â†’ Î²) : hatÎ± â†’ hatÎ² :=
  pkg.extend (Î¹' âˆ˜ f)

local notation "map" => pkg.map pkg'

variable (f : Î± â†’ Î²)

theorem uniformContinuous_map : UniformContinuous (map f) :=
  pkg.uniformContinuous_extend

@[continuity]
theorem continuous_map : Continuous (map f) :=
  pkg.continuous_extend

variable {f}

@[simp]
theorem map_coe (hf : UniformContinuous f) (a : Î±) : map f (Î¹ a) = Î¹' (f a) :=
  pkg.extend_coe (pkg'.uniformContinuous_coe.comp hf) a

theorem map_unique {f : Î± â†’ Î²} {g : hatÎ± â†’ hatÎ²} (hg : UniformContinuous g)
    (h : âˆ€ a, Î¹' (f a) = g (Î¹ a)) : map f = g :=
  pkg.funext (pkg.continuous_map _ _) hg.continuous <| by
    intro a
    change pkg.extend (Î¹' âˆ˜ f) _ = _
    simp_rw [Function.comp_def, h, â† comp_apply (f := g)]
    rw [pkg.extend_coe (hg.comp pkg.uniformContinuous_coe)]

@[simp]
theorem map_id : pkg.map pkg id = id :=
  pkg.map_unique pkg uniformContinuous_id fun _ => rfl

variable {Î³ : Type uÎ³} [UniformSpace Î³]

theorem extend_map [CompleteSpace Î³] [T0Space Î³] {f : Î² â†’ Î³} {g : Î± â†’ Î²}
    (hf : UniformContinuous f) (hg : UniformContinuous g) :
    pkg'.extend f âˆ˜ map g = pkg.extend (f âˆ˜ g) :=
  pkg.funext (pkg'.continuous_extend.comp (pkg.continuous_map pkg' _)) pkg.continuous_extend
    fun a => by
    rw [pkg.extend_coe (hf.comp hg), comp_apply, pkg.map_coe pkg' hg, pkg'.extend_coe hf]
    rfl

variable (pkg'' : AbstractCompletion.{vÎ³} Î³)

theorem map_comp {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : UniformContinuous g) (hf : UniformContinuous f) :
    pkg'.map pkg'' g âˆ˜ pkg.map pkg' f = pkg.map pkg'' (g âˆ˜ f) :=
  pkg.extend_map pkg' (pkg''.uniformContinuous_coe.comp hg) hf

/-- The uniform isomorphism between two completions of isomorphic uniform spaces. -/
def mapEquiv (e : Î± â‰ƒáµ¤ Î²) : hatÎ± â‰ƒáµ¤ hatÎ² where
  toFun := pkg.map pkg' e
  invFun := pkg'.map pkg e.symm
  uniformContinuous_toFun := uniformContinuous_map ..
  uniformContinuous_invFun := uniformContinuous_map ..
  left_inv := Function.leftInverse_iff_comp.2 <| by
    simp [map_comp _ _ _ e.symm.uniformContinuous e.uniformContinuous]
  right_inv := Function.rightInverse_iff_comp.2 <| by
    simp [map_comp _ _ _ e.uniformContinuous e.symm.uniformContinuous]

@[simp]
theorem mapEquiv_symm (e : Î± â‰ƒáµ¤ Î²) :
    (pkg.mapEquiv pkg' e).symm = pkg'.mapEquiv pkg e.symm := rfl

@[simp]
theorem mapEquiv_coe (e : Î± â‰ƒáµ¤ Î²) (a : Î±) : pkg.mapEquiv pkg' e (Î¹ a) = Î¹' (e a) :=
  pkg.map_coe pkg' e.uniformContinuous _

end MapSec

section Compare

-- We can now compare two completion packages for the same uniform space
variable (pkg' : AbstractCompletion.{vÎ±'} Î±)

/-- The comparison map between two completions of the same uniform space. -/
def compare : pkg.space â†’ pkg'.space :=
  pkg.extend pkg'.coe

theorem uniformContinuous_compare : UniformContinuous (pkg.compare pkg') :=
  pkg.uniformContinuous_extend

theorem compare_coe (a : Î±) : pkg.compare pkg' (pkg.coe a) = pkg'.coe a :=
  pkg.extend_coe pkg'.uniformContinuous_coe a

theorem inverse_compare : pkg.compare pkg' âˆ˜ pkg'.compare pkg = id := by
  have uc := pkg.uniformContinuous_compare pkg'
  have uc' := pkg'.uniformContinuous_compare pkg
  apply pkg'.funext (uc.comp uc').continuous continuous_id
  intro a
  rw [comp_apply, pkg'.compare_coe pkg, pkg.compare_coe pkg']
  rfl

/-- The uniform bijection between two completions of the same uniform space. -/
def compareEquiv : pkg.space â‰ƒáµ¤ pkg'.space where
  toFun := pkg.compare pkg'
  invFun := pkg'.compare pkg
  left_inv := congr_fun (pkg'.inverse_compare pkg)
  right_inv := congr_fun (pkg.inverse_compare pkg')
  uniformContinuous_toFun := uniformContinuous_compare _ _
  uniformContinuous_invFun := uniformContinuous_compare _ _

theorem uniformContinuous_compareEquiv : UniformContinuous (pkg.compareEquiv pkg') :=
  pkg.uniformContinuous_compare pkg'

theorem uniformContinuous_compareEquiv_symm : UniformContinuous (pkg.compareEquiv pkg').symm :=
  pkg'.uniformContinuous_compare pkg


open scoped Topology

/-Let `f : Î± â†’ Î³` be a continuous function between a uniform space `Î±` and a regular topological
space `Î³`, and let `pkg, pkg'` be two abstract completions of `Î±`. Then
if for every point `a : pkg` the filter `f.map (coeâ»Â¹ (ð“ a))` obtained by pushing forward with `f`
the preimage in `Î±` of `ð“ a` tends to `ð“ (f.extend a : Î²)`, then the comparison map
between `pkg` and `pkg'` composed with the extension of `f` to `pkg`` coincides with the
extension of `f` to `pkg'`. The situation is described in the following diagram, where the
two diagonal arrows are the extensions of `f` to the two different completions `pkg` and `pkg'`;
the statement of `compare_comp_eq_compare` is the commutativity of the right triangle.

```
`Î±^`=`pkg` â‰… `Î±^'`=`pkg'`   *here `â‰…` is `compare`*
  âˆ§     \        /
  |      \      /
  |       \    /
  |        V  âˆ¨
 Î± ---f---> Î³
```
-/
theorem compare_comp_eq_compare (Î³ : Type uÎ³) [TopologicalSpace Î³]
    [T3Space Î³] {f : Î± â†’ Î³} (cont_f : Continuous f) :
    letI := pkg.uniformStruct.toTopologicalSpace
    letI := pkg'.uniformStruct.toTopologicalSpace
    (âˆ€ a : pkg.space,
      Filter.Tendsto f (Filter.comap pkg.coe (ð“ a)) (ð“ ((pkg.isDenseInducing.extend f) a))) â†’
      pkg.isDenseInducing.extend f âˆ˜ pkg'.compare pkg = pkg'.isDenseInducing.extend f := by
  let _ := pkg'.uniformStruct
  let _ := pkg.uniformStruct
  intro h
  have (x : Î±) : (pkg.isDenseInducing.extend f âˆ˜ pkg'.compare pkg) (pkg'.coe x) = f x := by
    simp only [Function.comp_apply, compare_coe, IsDenseInducing.extend_eq _ cont_f]
  apply (IsDenseInducing.extend_unique (AbstractCompletion.isDenseInducing _) this
    (Continuous.comp _ (uniformContinuous_compare pkg' pkg).continuous )).symm
  apply IsDenseInducing.continuous_extend
  exact fun a â†¦ âŸ¨(pkg.isDenseInducing.extend f) a, h aâŸ©

end Compare

section Prod

variable (pkg' : AbstractCompletion.{vÎ²} Î²)

local notation "hatÎ²" => pkg'.space

local notation "Î¹'" => pkg'.coe

/-- Products of completions -/
protected def prod : AbstractCompletion (Î± Ã— Î²) where
  space := hatÎ± Ã— hatÎ²
  coe p := âŸ¨Î¹ p.1, Î¹' p.2âŸ©
  uniformStruct := inferInstance
  complete := inferInstance
  separation := inferInstance
  isUniformInducing := IsUniformInducing.prod pkg.isUniformInducing pkg'.isUniformInducing
  dense := pkg.dense.prodMap pkg'.dense

end Prod

section Extensionâ‚‚

variable (pkg' : AbstractCompletion.{vÎ²} Î²)

local notation "hatÎ²" => pkg'.space

local notation "Î¹'" => pkg'.coe

variable {Î³ : Type uÎ³} [UniformSpace Î³]

open Function

/-- Extend two variable map to completions. -/
protected def extendâ‚‚ (f : Î± â†’ Î² â†’ Î³) : hatÎ± â†’ hatÎ² â†’ Î³ :=
  curry <| (pkg.prod pkg').extend (uncurry f)

section T0Space

variable [T0Space Î³] {f : Î± â†’ Î² â†’ Î³}

theorem extensionâ‚‚_coe_coe (hf : UniformContinuous <| uncurry f) (a : Î±) (b : Î²) :
    pkg.extendâ‚‚ pkg' f (Î¹ a) (Î¹' b) = f a b :=
  show (pkg.prod pkg').extend (uncurry f) ((pkg.prod pkg').coe (a, b)) = uncurry f (a, b) from
    (pkg.prod pkg').extend_coe hf _

end T0Space

variable {f : Î± â†’ Î² â†’ Î³}
variable [CompleteSpace Î³] (f)

theorem uniformContinuous_extensionâ‚‚ : UniformContinuousâ‚‚ (pkg.extendâ‚‚ pkg' f) := by
  rw [uniformContinuousâ‚‚_def, AbstractCompletion.extendâ‚‚, uncurry_curry]
  apply uniformContinuous_extend

end Extensionâ‚‚

section Mapâ‚‚

variable (pkg' : AbstractCompletion Î²)

local notation "hatÎ²" => pkg'.space

local notation "Î¹'" => pkg'.coe

variable {Î³ : Type uÎ³} [UniformSpace Î³] (pkg'' : AbstractCompletion.{vÎ³} Î³)

local notation "hatÎ³" => pkg''.space

local notation "Î¹''" => pkg''.coe

local notation f " âˆ˜â‚‚ " g => bicompr f g

/-- Lift two variable maps to completions. -/
protected def mapâ‚‚ (f : Î± â†’ Î² â†’ Î³) : hatÎ± â†’ hatÎ² â†’ hatÎ³ :=
  pkg.extendâ‚‚ pkg' (pkg''.coe âˆ˜â‚‚ f)

theorem uniformContinuous_mapâ‚‚ (f : Î± â†’ Î² â†’ Î³) : UniformContinuousâ‚‚ (pkg.mapâ‚‚ pkg' pkg'' f) :=
  AbstractCompletion.uniformContinuous_extensionâ‚‚ pkg pkg' _

theorem continuous_mapâ‚‚ {Î´} [TopologicalSpace Î´] {f : Î± â†’ Î² â†’ Î³} {a : Î´ â†’ hatÎ±} {b : Î´ â†’ hatÎ²}
    (ha : Continuous a) (hb : Continuous b) :
    Continuous fun d : Î´ => pkg.mapâ‚‚ pkg' pkg'' f (a d) (b d) :=
  (pkg.uniformContinuous_mapâ‚‚ pkg' pkg'' f).continuous.compâ‚‚ ha hb

theorem mapâ‚‚_coe_coe (a : Î±) (b : Î²) (f : Î± â†’ Î² â†’ Î³) (hf : UniformContinuousâ‚‚ f) :
    pkg.mapâ‚‚ pkg' pkg'' f (Î¹ a) (Î¹' b) = Î¹'' (f a b) :=
  pkg.extensionâ‚‚_coe_coe (f := pkg''.coe âˆ˜â‚‚ f) pkg' (pkg''.uniformContinuous_coe.comp hf) a b

end Mapâ‚‚

end AbstractCompletion
