/-
Copyright (c) 2020 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Topology.UniformSpace.UniformConvergence

/-!
# Locally uniform convergence

We define a sequence of functions `F‚Çô` to *converge locally uniformly* to a limiting function `f`
with respect to a filter `p`, spelled `TendstoLocallyUniformly F f p`, if for any `x ‚àà s` and any
entourage of the diagonal `u`, there is a neighbourhood `v` of `x` such that `p`-eventually we have
`(f y, F‚Çô y) ‚àà u` for all `y ‚àà v`.

It is important to note that this definition is somewhat non-standard; it is **not** in general
equivalent to "every point has a neighborhood on which the convergence is uniform", which is the
definition more commonly encountered in the literature. The reason is that in our definition the
neighborhood `v` of `x` can depend on the entourage `u`; so our condition is *a priori* weaker than
the usual one, although the two conditions are equivalent if the domain is locally compact. See
`tendstoLocallyUniformlyOn_of_forall_exists_nhds` for the one-way implication; the equivalence
assuming local compactness is part of `tendstoLocallyUniformlyOn_TFAE`.

We adopt this weaker condition because it is more general but appears to be sufficient for
the standard applications of locally-uniform convergence (in particular, for proving that a
locally-uniform limit of continuous functions is continuous).

We also define variants for locally uniform convergence on a subset, called
`TendstoLocallyUniformlyOn F f p s`.

## Tags

Uniform limit, uniform convergence, tends uniformly to
-/

noncomputable section

open Topology Uniformity Filter Set Uniform

variable {Œ± Œ≤ Œ≥ Œπ : Type*} [TopologicalSpace Œ±] [UniformSpace Œ≤]
variable {F : Œπ ‚Üí Œ± ‚Üí Œ≤} {f : Œ± ‚Üí Œ≤} {s s' : Set Œ±} {x : Œ±} {p : Filter Œπ}

/-- A sequence of functions `F‚Çô` converges locally uniformly on a set `s` to a limiting function
`f` with respect to a filter `p` if, for any entourage of the diagonal `u`, for any `x ‚àà s`, one
has `p`-eventually `(f y, F‚Çô y) ‚àà u` for all `y` in a neighborhood of `x` in `s`. -/
def TendstoLocallyUniformlyOn (F : Œπ ‚Üí Œ± ‚Üí Œ≤) (f : Œ± ‚Üí Œ≤) (p : Filter Œπ) (s : Set Œ±) :=
  ‚àÄ u ‚àà ùì§ Œ≤, ‚àÄ x ‚àà s, ‚àÉ t ‚àà ùìù[s] x, ‚àÄ·∂† n in p, ‚àÄ y ‚àà t, (f y, F n y) ‚àà u

/-- A sequence of functions `F‚Çô` converges locally uniformly to a limiting function `f` with respect
to a filter `p` if, for any entourage of the diagonal `u`, for any `x`, one has `p`-eventually
`(f y, F‚Çô y) ‚àà u` for all `y` in a neighborhood of `x`. -/
def TendstoLocallyUniformly (F : Œπ ‚Üí Œ± ‚Üí Œ≤) (f : Œ± ‚Üí Œ≤) (p : Filter Œπ) :=
  ‚àÄ u ‚àà ùì§ Œ≤, ‚àÄ x : Œ±, ‚àÉ t ‚àà ùìù x, ‚àÄ·∂† n in p, ‚àÄ y ‚àà t, (f y, F n y) ‚àà u

theorem tendstoLocallyUniformlyOn_univ :
    TendstoLocallyUniformlyOn F f p univ ‚Üî TendstoLocallyUniformly F f p := by
  simp [TendstoLocallyUniformlyOn, TendstoLocallyUniformly, nhdsWithin_univ]

theorem tendstoLocallyUniformlyOn_iff_forall_tendsto :
    TendstoLocallyUniformlyOn F f p s ‚Üî
      ‚àÄ x ‚àà s, Tendsto (fun y : Œπ √ó Œ± => (f y.2, F y.1 y.2)) (p √óÀ¢ ùìù[s] x) (ùì§ Œ≤) :=
  forall‚ÇÇ_swap.trans <| forall‚ÇÑ_congr fun _ _ _ _ => by
    simp_rw [mem_map, mem_prod_iff_right, mem_preimage]

nonrec theorem IsOpen.tendstoLocallyUniformlyOn_iff_forall_tendsto (hs : IsOpen s) :
    TendstoLocallyUniformlyOn F f p s ‚Üî
      ‚àÄ x ‚àà s, Tendsto (fun y : Œπ √ó Œ± => (f y.2, F y.1 y.2)) (p √óÀ¢ ùìù x) (ùì§ Œ≤) :=
  tendstoLocallyUniformlyOn_iff_forall_tendsto.trans <| forall‚ÇÇ_congr fun x hx => by
    rw [hs.nhdsWithin_eq hx]

theorem tendstoLocallyUniformly_iff_forall_tendsto :
    TendstoLocallyUniformly F f p ‚Üî
      ‚àÄ x, Tendsto (fun y : Œπ √ó Œ± => (f y.2, F y.1 y.2)) (p √óÀ¢ ùìù x) (ùì§ Œ≤) := by
  simp [‚Üê tendstoLocallyUniformlyOn_univ, isOpen_univ.tendstoLocallyUniformlyOn_iff_forall_tendsto]

theorem tendstoLocallyUniformlyOn_iff_tendstoLocallyUniformly_comp_coe :
    TendstoLocallyUniformlyOn F f p s ‚Üî
      TendstoLocallyUniformly (fun i (x : s) => F i x) (f ‚àò (‚Üë)) p := by
  simp only [tendstoLocallyUniformly_iff_forall_tendsto, Subtype.forall', tendsto_map'_iff,
    tendstoLocallyUniformlyOn_iff_forall_tendsto, ‚Üê map_nhds_subtype_val, prod_map_right]; rfl

protected theorem TendstoUniformlyOn.tendstoLocallyUniformlyOn (h : TendstoUniformlyOn F f p s) :
    TendstoLocallyUniformlyOn F f p s := fun u hu _ _ =>
  ‚ü®s, self_mem_nhdsWithin, by simpa using h u hu‚ü©

protected theorem TendstoUniformly.tendstoLocallyUniformly (h : TendstoUniformly F f p) :
    TendstoLocallyUniformly F f p := fun u hu _ => ‚ü®univ, univ_mem, by simpa using h u hu‚ü©

theorem TendstoLocallyUniformlyOn.mono (h : TendstoLocallyUniformlyOn F f p s) (h' : s' ‚äÜ s) :
    TendstoLocallyUniformlyOn F f p s' := by
  intro u hu x hx
  rcases h u hu x (h' hx) with ‚ü®t, ht, H‚ü©
  exact ‚ü®t, nhdsWithin_mono x h' ht, H.mono fun n => id‚ü©

theorem tendstoLocallyUniformlyOn_iUnion {Œπ' : Sort*} {S : Œπ' ‚Üí Set Œ±} (hS : ‚àÄ i, IsOpen (S i))
    (h : ‚àÄ i, TendstoLocallyUniformlyOn F f p (S i)) :
    TendstoLocallyUniformlyOn F f p (‚ãÉ i, S i) :=
  (isOpen_iUnion hS).tendstoLocallyUniformlyOn_iff_forall_tendsto.2 fun _x hx =>
    let ‚ü®i, hi‚ü© := mem_iUnion.1 hx
    (hS i).tendstoLocallyUniformlyOn_iff_forall_tendsto.1 (h i) _ hi

theorem tendstoLocallyUniformlyOn_biUnion {s : Set Œ≥} {S : Œ≥ ‚Üí Set Œ±} (hS : ‚àÄ i ‚àà s, IsOpen (S i))
    (h : ‚àÄ i ‚àà s, TendstoLocallyUniformlyOn F f p (S i)) :
    TendstoLocallyUniformlyOn F f p (‚ãÉ i ‚àà s, S i) :=
  tendstoLocallyUniformlyOn_iUnion (fun i => isOpen_iUnion (hS i))
    fun i ‚Ü¶ tendstoLocallyUniformlyOn_iUnion (hS i) (h i)

theorem tendstoLocallyUniformlyOn_sUnion (S : Set (Set Œ±)) (hS : ‚àÄ s ‚àà S, IsOpen s)
    (h : ‚àÄ s ‚àà S, TendstoLocallyUniformlyOn F f p s) : TendstoLocallyUniformlyOn F f p (‚ãÉ‚ÇÄ S) := by
  rw [sUnion_eq_biUnion]
  exact tendstoLocallyUniformlyOn_biUnion hS h

theorem TendstoLocallyUniformlyOn.union (hs‚ÇÅ : IsOpen s) (hs‚ÇÇ : IsOpen s')
    (h‚ÇÅ : TendstoLocallyUniformlyOn F f p s) (h‚ÇÇ : TendstoLocallyUniformlyOn F f p s') :
    TendstoLocallyUniformlyOn F f p (s ‚à™ s') := by
  rw [‚Üê sUnion_pair]
  refine tendstoLocallyUniformlyOn_sUnion _ ?_ ?_ <;> simp [*]

protected theorem TendstoLocallyUniformly.tendstoLocallyUniformlyOn
    (h : TendstoLocallyUniformly F f p) : TendstoLocallyUniformlyOn F f p s :=
  (tendstoLocallyUniformlyOn_univ.mpr h).mono (subset_univ _)

/-- On a compact space, locally uniform convergence is just uniform convergence. -/
theorem tendstoLocallyUniformly_iff_tendstoUniformly_of_compactSpace [CompactSpace Œ±] :
    TendstoLocallyUniformly F f p ‚Üî TendstoUniformly F f p := by
  refine ‚ü®fun h V hV => ?_, TendstoUniformly.tendstoLocallyUniformly‚ü©
  choose U hU using h V hV
  obtain ‚ü®t, ht‚ü© := isCompact_univ.elim_nhds_subcover' (fun k _ => U k) fun k _ => (hU k).1
  replace hU := fun x : t => (hU x).2
  rw [‚Üê eventually_all] at hU
  refine hU.mono fun i hi x => ?_
  specialize ht (mem_univ x)
  simp only [exists_prop, mem_iUnion, SetCoe.exists, exists_and_right] at ht
  obtain ‚ü®y, ‚ü®hy‚ÇÅ, hy‚ÇÇ‚ü©, hy‚ÇÉ‚ü© := ht
  exact hi ‚ü®‚ü®y, hy‚ÇÅ‚ü©, hy‚ÇÇ‚ü© x hy‚ÇÉ

/-- For a compact set `s`, locally uniform convergence on `s` is just uniform convergence on `s`. -/
theorem tendstoLocallyUniformlyOn_iff_tendstoUniformlyOn_of_compact (hs : IsCompact s) :
    TendstoLocallyUniformlyOn F f p s ‚Üî TendstoUniformlyOn F f p s := by
  haveI : CompactSpace s := isCompact_iff_compactSpace.mp hs
  refine ‚ü®fun h => ?_, TendstoUniformlyOn.tendstoLocallyUniformlyOn‚ü©
  rwa [tendstoLocallyUniformlyOn_iff_tendstoLocallyUniformly_comp_coe,
    tendstoLocallyUniformly_iff_tendstoUniformly_of_compactSpace, ‚Üê
    tendstoUniformlyOn_iff_tendstoUniformly_comp_coe] at h

theorem TendstoLocallyUniformlyOn.comp [TopologicalSpace Œ≥] {t : Set Œ≥}
    (h : TendstoLocallyUniformlyOn F f p s) (g : Œ≥ ‚Üí Œ±) (hg : MapsTo g t s)
    (cg : ContinuousOn g t) : TendstoLocallyUniformlyOn (fun n => F n ‚àò g) (f ‚àò g) p t := by
  intro u hu x hx
  rcases h u hu (g x) (hg hx) with ‚ü®a, ha, H‚ü©
  have : g ‚Åª¬π' a ‚àà ùìù[t] x :=
    (cg x hx).preimage_mem_nhdsWithin' (nhdsWithin_mono (g x) hg.image_subset ha)
  exact ‚ü®g ‚Åª¬π' a, this, H.mono fun n hn y hy => hn _ hy‚ü©

theorem TendstoLocallyUniformly.comp [TopologicalSpace Œ≥] (h : TendstoLocallyUniformly F f p)
    (g : Œ≥ ‚Üí Œ±) (cg : Continuous g) : TendstoLocallyUniformly (fun n => F n ‚àò g) (f ‚àò g) p := by
  rw [‚Üê tendstoLocallyUniformlyOn_univ] at h ‚ä¢
  rw [‚Üê continuousOn_univ] at cg
  exact h.comp _ (mapsTo_univ _ _) cg

/-- If every `x ‚àà s` has a neighbourhood within `s` on which `F i` tends uniformly to `f`, then
`F i` tends locally uniformly on `s` to `f`.

Note this is **not** a tautology, since our definition of `TendstoLocallyUniformlyOn` is slightly
more general (although the conditions are equivalent if `Œ≤` is locally compact and `s` is open,
see `tendstoLocallyUniformlyOn_TFAE`). -/
lemma tendstoLocallyUniformlyOn_of_forall_exists_nhds
    (h : ‚àÄ x ‚àà s, ‚àÉ t ‚àà ùìù[s] x, TendstoUniformlyOn F f p t) :
    TendstoLocallyUniformlyOn F f p s := by
  refine tendstoLocallyUniformlyOn_iff_forall_tendsto.mpr fun x hx ‚Ü¶ ?_
  obtain ‚ü®t, ht, htr‚ü© := h x hx
  rw [tendstoUniformlyOn_iff_tendsto] at htr
  exact htr.mono_left <| prod_mono_right _ <| le_principal_iff.mpr ht

@[deprecated (since := "2025-05-22")] alias tendstoLocallyUniformlyOn_of_forall_exists_nhd :=
  tendstoLocallyUniformlyOn_of_forall_exists_nhds

/-- If every `x` has a neighbourhood on which `F i` tends uniformly to `f`, then `F i` tends
locally uniformly to `f`. (Special case of `tendstoLocallyUniformlyOn_of_forall_exists_nhds`
where `s = univ`.) -/
lemma tendstoLocallyUniformly_of_forall_exists_nhds
    (h : ‚àÄ x, ‚àÉ t ‚àà ùìù x, TendstoUniformlyOn F f p t) :
    TendstoLocallyUniformly F f p :=
  tendstoLocallyUniformlyOn_univ.mp
    <| tendstoLocallyUniformlyOn_of_forall_exists_nhds (by simpa using h)

@[deprecated (since := "2025-05-22")] alias tendstoLocallyUniformly_of_forall_exists_nhd :=
  tendstoLocallyUniformly_of_forall_exists_nhds

theorem tendstoLocallyUniformlyOn_TFAE [LocallyCompactSpace Œ±] (G : Œπ ‚Üí Œ± ‚Üí Œ≤) (g : Œ± ‚Üí Œ≤)
    (p : Filter Œπ) (hs : IsOpen s) :
    List.TFAE [
      TendstoLocallyUniformlyOn G g p s,
      ‚àÄ K, K ‚äÜ s ‚Üí IsCompact K ‚Üí TendstoUniformlyOn G g p K,
      ‚àÄ x ‚àà s, ‚àÉ v ‚àà ùìù[s] x, TendstoUniformlyOn G g p v] := by
  tfae_have 1 ‚Üí 2
  | h, K, hK1, hK2 =>
    (tendstoLocallyUniformlyOn_iff_tendstoUniformlyOn_of_compact hK2).mp (h.mono hK1)
  tfae_have 2 ‚Üí 3
  | h, x, hx => by
    obtain ‚ü®K, ‚ü®hK1, hK2‚ü©, hK3‚ü© := (compact_basis_nhds x).mem_iff.mp (hs.mem_nhds hx)
    exact ‚ü®K, nhdsWithin_le_nhds hK1, h K hK3 hK2‚ü©
  tfae_have 3 ‚Üí 1
  | h, u, hu, x, hx => by
    obtain ‚ü®v, hv1, hv2‚ü© := h x hx
    exact ‚ü®v, hv1, hv2 u hu‚ü©
  tfae_finish

theorem tendstoLocallyUniformlyOn_iff_forall_isCompact [LocallyCompactSpace Œ±] (hs : IsOpen s) :
    TendstoLocallyUniformlyOn F f p s ‚Üî ‚àÄ K, K ‚äÜ s ‚Üí IsCompact K ‚Üí TendstoUniformlyOn F f p K :=
  (tendstoLocallyUniformlyOn_TFAE F f p hs).out 0 1

lemma tendstoLocallyUniformly_iff_forall_isCompact [LocallyCompactSpace Œ±] :
    TendstoLocallyUniformly F f p ‚Üî ‚àÄ K : Set Œ±, IsCompact K ‚Üí TendstoUniformlyOn F f p K := by
  simp only [‚Üê tendstoLocallyUniformlyOn_univ,
    tendstoLocallyUniformlyOn_iff_forall_isCompact isOpen_univ, Set.subset_univ, forall_true_left]

theorem tendstoLocallyUniformlyOn_iff_filter :
    TendstoLocallyUniformlyOn F f p s ‚Üî ‚àÄ x ‚àà s, TendstoUniformlyOnFilter F f p (ùìù[s] x) := by
  simp only [TendstoUniformlyOnFilter, eventually_prod_iff]
  constructor
  ¬∑ rintro h x hx u hu
    obtain ‚ü®s, hs1, hs2‚ü© := h u hu x hx
    exact ‚ü®_, hs2, _, eventually_of_mem hs1 fun x => id, fun hi y hy => hi y hy‚ü©
  ¬∑ rintro h u hu x hx
    obtain ‚ü®pa, hpa, pb, hpb, h‚ü© := h x hx u hu
    exact ‚ü®pb, hpb, eventually_of_mem hpa fun i hi y hy => h hi hy‚ü©

theorem tendstoLocallyUniformly_iff_filter :
    TendstoLocallyUniformly F f p ‚Üî ‚àÄ x, TendstoUniformlyOnFilter F f p (ùìù x) := by
  simpa [‚Üê tendstoLocallyUniformlyOn_univ, ‚Üê nhdsWithin_univ] using
    @tendstoLocallyUniformlyOn_iff_filter _ _ _ _ _ F f univ p

theorem TendstoLocallyUniformlyOn.tendsto_at (hf : TendstoLocallyUniformlyOn F f p s) {a : Œ±}
    (ha : a ‚àà s) : Tendsto (fun i => F i a) p (ùìù (f a)) := by
  refine ((tendstoLocallyUniformlyOn_iff_filter.mp hf) a ha).tendsto_at ?_
  simpa only [Filter.principal_singleton] using pure_le_nhdsWithin ha

theorem TendstoLocallyUniformlyOn.unique [p.NeBot] [T2Space Œ≤] {g : Œ± ‚Üí Œ≤}
    (hf : TendstoLocallyUniformlyOn F f p s) (hg : TendstoLocallyUniformlyOn F g p s) :
    s.EqOn f g := fun _a ha => tendsto_nhds_unique (hf.tendsto_at ha) (hg.tendsto_at ha)

theorem TendstoLocallyUniformlyOn.congr {G : Œπ ‚Üí Œ± ‚Üí Œ≤} (hf : TendstoLocallyUniformlyOn F f p s)
    (hg : ‚àÄ n, s.EqOn (F n) (G n)) : TendstoLocallyUniformlyOn G f p s := by
  rintro u hu x hx
  obtain ‚ü®t, ht, h‚ü© := hf u hu x hx
  refine ‚ü®s ‚à© t, inter_mem self_mem_nhdsWithin ht, ?_‚ü©
  filter_upwards [h] with i hi y hy using hg i hy.1 ‚ñ∏ hi y hy.2

theorem TendstoLocallyUniformlyOn.congr_right {g : Œ± ‚Üí Œ≤} (hf : TendstoLocallyUniformlyOn F f p s)
    (hg : s.EqOn f g) : TendstoLocallyUniformlyOn F g p s := by
  rintro u hu x hx
  obtain ‚ü®t, ht, h‚ü© := hf u hu x hx
  refine ‚ü®s ‚à© t, inter_mem self_mem_nhdsWithin ht, ?_‚ü©
  filter_upwards [h] with i hi y hy using hg hy.1 ‚ñ∏ hi y hy.2
