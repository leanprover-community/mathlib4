/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Patrick Massot
-/
import Mathlib.Tactic.ApplyFun
import Mathlib.Topology.UniformSpace.Basic
import Mathlib.Topology.Separation

#align_import topology.uniform_space.separation from "leanprover-community/mathlib"@"0c1f285a9f6e608ae2bdffa3f993eafb01eba829"

/-!
# Hausdorff properties of uniform spaces. Separation quotient.

This file studies uniform spaces whose underlying topological spaces are separated
(also known as Hausdorff or T‚ÇÇ).
This turns out to be equivalent to asking that the intersection of all entourages
is the diagonal only. This condition actually implies the stronger separation property
that the space is T‚ÇÉ, hence those conditions are equivalent for topologies coming from
a uniform structure.

More generally, the intersection `ùì¢ X` of all entourages of `X`, which has type `Set (X √ó X)` is an
equivalence relation on `X`. Points which are equivalent under the relation are basically
undistinguishable from the point of view of the uniform structure. For instance any uniformly
continuous function will send equivalent points to the same value.

The quotient `SeparationQuotient X` of `X` by `ùì¢ X` has a natural uniform structure which is
separated, and satisfies a universal property: every uniformly continuous function
from `X` to a separated uniform space uniquely factors through `SeparationQuotient X`.
As usual, this allows to turn `SeparationQuotient` into a functor (but we don't use the
category theory library in this file).

These notions admit relative versions, one can ask that `s : Set X` is separated, this
is equivalent to asking that the uniform structure induced on `s` is separated.

## Main definitions

* `separationRel X : Set (X √ó X)`: the separation relation
* `SeparatedSpace X`: a predicate class asserting that `X` is separated
* `SeparationQuotient X`: the maximal separated quotient of `X`.
* `SeparationQuotient.lift f`: factors a map `f : X ‚Üí Y` through the separation quotient of `X`.
* `SeparationQuotient.map f`: turns a map `f : X ‚Üí Y` into a map between the separation quotients
  of `X` and `Y`.

## Main results

* `separated_iff_t2`: the equivalence between being separated and being Hausdorff for uniform
  spaces.
* `SeparationQuotient.uniformContinuous_lift`: factoring a uniformly continuous map through the
  separation quotient gives a uniformly continuous map.
* `SeparationQuotient.uniformContinuous_map`: maps induced between separation quotients are
  uniformly continuous.

##¬†Notations

Localized in `uniformity`, we have the notation `ùì¢ X` for the separation relation
on a uniform space `X`,

## Implementation notes

The separation setoid `separationSetoid` is not declared as a global instance.
It is made a local instance while building the theory of `SeparationQuotient`.
The factored map `SeparationQuotient.lift f` is defined without imposing any condition on
`f`, but returns junk if `f` is not uniformly continuous (constant junk hence it is always
uniformly continuous).

-/

open Filter Set Function Topology Uniformity UniformSpace
open scoped Classical

noncomputable section

universe u v w

variable {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w}
variable [UniformSpace Œ±] [UniformSpace Œ≤] [UniformSpace Œ≥]

/-!
### Separated uniform spaces
-/

instance (priority := 100) UniformSpace.to_regularSpace : RegularSpace Œ± :=
  RegularSpace.ofBasis
    (fun a => by
      rw [nhds_eq_comap_uniformity]
      exact uniformity_hasBasis_closed.comap _)
    fun a V hV => by exact hV.2.preimage <| continuous_const.prod_mk continuous_id
#align uniform_space.to_regular_space UniformSpace.to_regularSpace

-- porting note: todo: use `Inseparable`
/-- The separation relation is the intersection of all entourages.
  Two points which are related by the separation relation are "indistinguishable"
  according to the uniform structure. -/
def separationRel (Œ± : Type u) [UniformSpace Œ±] := (ùì§ Œ±).ker
#align separation_rel separationRel

@[inherit_doc]
scoped[Uniformity] notation "ùì¢" => separationRel

theorem separated_equiv : Equivalence fun x y => (x, y) ‚àà ùì¢ Œ± :=
  ‚ü®fun _ _ => refl_mem_uniformity, fun h _s hs => h _ (symm_le_uniformity hs),
    fun {x y z} (hxy : (x, y) ‚àà ùì¢ Œ±) (hyz : (y, z) ‚àà ùì¢ Œ±) s (hs : s ‚àà ùì§ Œ±) =>
    let ‚ü®t, ht, (h_ts : compRel t t ‚äÜ s)‚ü© := comp_mem_uniformity_sets hs
    h_ts <| show (x, z) ‚àà compRel t t from ‚ü®y, hxy t ht, hyz t ht‚ü©‚ü©
#align separated_equiv separated_equiv

theorem Filter.HasBasis.mem_separationRel {Œπ : Sort*} {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí Set (Œ± √ó Œ±)}
    (h : (ùì§ Œ±).HasBasis p s) {a : Œ± √ó Œ±} : a ‚àà ùì¢ Œ± ‚Üî ‚àÄ i, p i ‚Üí a ‚àà s i :=
  h.forall_mem_mem
#align filter.has_basis.mem_separation_rel Filter.HasBasis.mem_separationRel

theorem separationRel_iff_specializes {a b : Œ±} : (a, b) ‚àà ùì¢ Œ± ‚Üî a ‚§≥ b := by
  simp only [(ùì§ Œ±).basis_sets.mem_separationRel, id, mem_setOf_eq,
    (nhds_basis_uniformity (ùì§ Œ±).basis_sets).specializes_iff]
#align separation_rel_iff_specializes separationRel_iff_specializes

theorem separationRel_iff_inseparable {a b : Œ±} : (a, b) ‚àà ùì¢ Œ± ‚Üî Inseparable a b :=
  separationRel_iff_specializes.trans specializes_iff_inseparable
#align separation_rel_iff_inseparable separationRel_iff_inseparable

/-- A uniform space is separated if its separation relation is trivial (each point
is related only to itself). -/
class SeparatedSpace (Œ± : Type u) [UniformSpace Œ±] : Prop where
  /-- The separation relation is equal to the diagonal `idRel`. -/
  out : ùì¢ Œ± = idRel
#align separated_space SeparatedSpace

theorem separatedSpace_iff {Œ± : Type u} [UniformSpace Œ±] : SeparatedSpace Œ± ‚Üî ùì¢ Œ± = idRel :=
  ‚ü®fun h => h.1, fun h => ‚ü®h‚ü©‚ü©
#align separated_space_iff separatedSpace_iff

theorem separated_def {Œ± : Type u} [UniformSpace Œ±] :
    SeparatedSpace Œ± ‚Üî ‚àÄ x y, (‚àÄ r ‚àà ùì§ Œ±, (x, y) ‚àà r) ‚Üí x = y := by
  simp only [separatedSpace_iff, Set.ext_iff, Prod.forall, mem_idRel, separationRel, mem_sInter]
  exact forall‚ÇÇ_congr fun _ _ => ‚ü®Iff.mp, fun h => ‚ü®h, fun H U hU => H ‚ñ∏ refl_mem_uniformity hU‚ü©‚ü©
#align separated_def separated_def

theorem separated_def' {Œ± : Type u} [UniformSpace Œ±] :
    SeparatedSpace Œ± ‚Üî ‚àÄ x y, x ‚â† y ‚Üí ‚àÉ r ‚àà ùì§ Œ±, (x, y) ‚àâ r :=
  separated_def.trans <| forall‚ÇÇ_congr fun x y => by rw [‚Üê not_imp_not]; simp [not_forall]
#align separated_def' separated_def'

theorem eq_of_uniformity {Œ± : Type*} [UniformSpace Œ±] [SeparatedSpace Œ±] {x y : Œ±}
    (h : ‚àÄ {V}, V ‚àà ùì§ Œ± ‚Üí (x, y) ‚àà V) : x = y :=
  separated_def.mp ‚ÄπSeparatedSpace Œ±‚Ä∫ x y fun _ => h
#align eq_of_uniformity eq_of_uniformity

theorem eq_of_uniformity_basis {Œ± : Type*} [UniformSpace Œ±] [SeparatedSpace Œ±] {Œπ : Type*}
    {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí Set (Œ± √ó Œ±)} (hs : (ùì§ Œ±).HasBasis p s) {x y : Œ±}
    (h : ‚àÄ {i}, p i ‚Üí (x, y) ‚àà s i) : x = y :=
  eq_of_uniformity fun V_in => let ‚ü®_, hi, H‚ü© := hs.mem_iff.mp V_in; H (h hi)
#align eq_of_uniformity_basis eq_of_uniformity_basis

theorem eq_of_forall_symmetric {Œ± : Type*} [UniformSpace Œ±] [SeparatedSpace Œ±] {x y : Œ±}
    (h : ‚àÄ {V}, V ‚àà ùì§ Œ± ‚Üí SymmetricRel V ‚Üí (x, y) ‚àà V) : x = y :=
  eq_of_uniformity_basis hasBasis_symmetric (by simpa [and_imp])
#align eq_of_forall_symmetric eq_of_forall_symmetric

theorem eq_of_clusterPt_uniformity [SeparatedSpace Œ±] {x y : Œ±} (h : ClusterPt (x, y) (ùì§ Œ±)) :
    x = y :=
  eq_of_uniformity_basis uniformity_hasBasis_closed fun ‚ü®hV, hVc‚ü© =>
    isClosed_iff_clusterPt.1 hVc _ <| h.mono <| le_principal_iff.2 hV
#align eq_of_cluster_pt_uniformity eq_of_clusterPt_uniformity

theorem idRel_sub_separationRel (Œ± : Type*) [UniformSpace Œ±] : idRel ‚äÜ ùì¢ Œ± := by
  unfold separationRel
  rw [idRel_subset]
  intro x
  suffices ‚àÄ t ‚àà ùì§ Œ±, (x, x) ‚àà t by simpa only [refl_mem_uniformity]
  exact fun t => refl_mem_uniformity
#align id_rel_sub_separation_relation idRel_sub_separationRel

theorem separationRel_comap {f : Œ± ‚Üí Œ≤}
    (h : ‚ÄπUniformSpace Œ±‚Ä∫ = UniformSpace.comap f ‚ÄπUniformSpace Œ≤‚Ä∫) :
    ùì¢ Œ± = Prod.map f f ‚Åª¬π' ùì¢ Œ≤ := by
  subst h
  dsimp [separationRel]
  simp_rw [uniformity_comap, ((ùì§ Œ≤).comap_hasBasis $ Prod.map f f).ker, ker_def, preimage_iInter]
#align separation_rel_comap separationRel_comap

protected theorem Filter.HasBasis.separationRel {Œπ : Sort*} {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí Set (Œ± √ó Œ±)}
    (h : HasBasis (ùì§ Œ±) p s) : ùì¢ Œ± = ‚ãÇ (i) (_ : p i), s i := by
  unfold separationRel
  rw [h.ker]
#align filter.has_basis.separation_rel Filter.HasBasis.separationRel

theorem separationRel_eq_inter_closure : ùì¢ Œ± = ‚ãÇ‚ÇÄ (closure '' (ùì§ Œ±).sets) := by
  simp [uniformity_hasBasis_closure.separationRel]
#align separation_rel_eq_inter_closure separationRel_eq_inter_closure

theorem isClosed_separationRel : IsClosed (ùì¢ Œ±) := by
  rw [separationRel_eq_inter_closure]
  apply isClosed_sInter
  rintro _ ‚ü®t, -, rfl‚ü©
  exact isClosed_closure
#align is_closed_separation_rel isClosed_separationRel

theorem separated_iff_t2 : SeparatedSpace Œ± ‚Üî T2Space Œ± := by
  constructor <;> intro h
  ¬∑ rw [t2_iff_isClosed_diagonal, ‚Üê show ùì¢ Œ± = diagonal Œ± from h.1]
    exact isClosed_separationRel
  ¬∑ rw [separated_def']
    intro x y hxy
    rcases t2_separation hxy with ‚ü®u, v, uo, -, hx, hy, h‚ü©
    rcases isOpen_iff_ball_subset.1 uo x hx with ‚ü®r, hrU, hr‚ü©
    exact ‚ü®r, hrU, fun H => h.le_bot ‚ü®hr H, hy‚ü©‚ü©
#align separated_iff_t2 separated_iff_t2

-- see Note [lower instance priority]
instance (priority := 100) separated_t3 [SeparatedSpace Œ±] : T3Space Œ± :=
  haveI := separated_iff_t2.mp ‚Äπ_‚Ä∫
  ‚ü®‚ü©
#align separated_t3 separated_t3

instance Subtype.separatedSpace [SeparatedSpace Œ±] (s : Set Œ±) : SeparatedSpace s :=
  separated_iff_t2.mpr inferInstance
#align subtype.separated_space Subtype.separatedSpace

theorem isClosed_of_spaced_out [SeparatedSpace Œ±] {V‚ÇÄ : Set (Œ± √ó Œ±)} (V‚ÇÄ_in : V‚ÇÄ ‚àà ùì§ Œ±) {s : Set Œ±}
    (hs : s.Pairwise fun x y => (x, y) ‚àâ V‚ÇÄ) : IsClosed s := by
  rcases comp_symm_mem_uniformity_sets V‚ÇÄ_in with ‚ü®V‚ÇÅ, V‚ÇÅ_in, V‚ÇÅ_symm, h_comp‚ü©
  apply isClosed_of_closure_subset
  intro x hx
  rw [mem_closure_iff_ball] at hx
  rcases hx V‚ÇÅ_in with ‚ü®y, hy, hy'‚ü©
  suffices x = y by rwa [this]
  apply eq_of_forall_symmetric
  intro V V_in _
  rcases hx (inter_mem V‚ÇÅ_in V_in) with ‚ü®z, hz, hz'‚ü©
  obtain rfl : z = y := by
    by_contra hzy
    exact hs hz' hy' hzy (h_comp <| mem_comp_of_mem_ball V‚ÇÅ_symm (ball_inter_left x _ _ hz) hy)
  exact ball_inter_right x _ _ hz
#align is_closed_of_spaced_out isClosed_of_spaced_out

theorem isClosed_range_of_spaced_out {Œπ} [SeparatedSpace Œ±] {V‚ÇÄ : Set (Œ± √ó Œ±)} (V‚ÇÄ_in : V‚ÇÄ ‚àà ùì§ Œ±)
    {f : Œπ ‚Üí Œ±} (hf : Pairwise fun x y => (f x, f y) ‚àâ V‚ÇÄ) : IsClosed (range f) :=
  isClosed_of_spaced_out V‚ÇÄ_in <| by
    rintro _ ‚ü®x, rfl‚ü© _ ‚ü®y, rfl‚ü© h
    exact hf (ne_of_apply_ne f h)
#align is_closed_range_of_spaced_out isClosed_range_of_spaced_out

/-!
### Separation quotient
-/

namespace UniformSpace

/-- The separation relation of a uniform space seen as a setoid. -/
def separationSetoid (Œ± : Type u) [UniformSpace Œ±] : Setoid Œ± :=
  ‚ü®fun x y => (x, y) ‚àà ùì¢ Œ±, separated_equiv‚ü©
#align uniform_space.separation_setoid UniformSpace.separationSetoid

attribute [local instance] separationSetoid

instance separationSetoid.uniformSpace {Œ± : Type u} [UniformSpace Œ±] :
    UniformSpace (Quotient (separationSetoid Œ±)) where
  toTopologicalSpace := instTopologicalSpaceQuotient
  uniformity := map (fun p : Œ± √ó Œ± => (‚ü¶p.1‚üß, ‚ü¶p.2‚üß)) (ùì§ Œ±)
  refl := le_trans (by simp [Quotient.exists_rep]) (Filter.map_mono refl_le_uniformity)
  symm := tendsto_map' <| tendsto_map.comp tendsto_swap_uniformity
  comp s hs := by
    rcases comp_open_symm_mem_uniformity_sets hs with ‚ü®U, hU, hUo, -, hUs‚ü©
    refine' mem_of_superset (mem_lift' <| image_mem_map hU) ?_
    simp only [subset_def, Prod.forall, mem_compRel, mem_image, Prod.ext_iff]
    rintro _ _ ‚ü®_, ‚ü®‚ü®x, y‚ü©, hxyU, rfl, rfl‚ü©, ‚ü®‚ü®y', z‚ü©, hyzU, hy, rfl‚ü©‚ü©
    have : y' ‚§≥ y := separationRel_iff_specializes.1 (Quotient.exact hy)
    exact @hUs (x, z) ‚ü®y', this.mem_open (UniformSpace.isOpen_ball _ hUo) hxyU, hyzU‚ü©
  isOpen_uniformity s := isOpen_coinduced.trans <| by
    simp only [_root_.isOpen_uniformity, Quotient.forall, mem_map', mem_setOf_eq]
    refine forall‚ÇÇ_congr fun x _ => ‚ü®fun h => ?_, fun h => mem_of_superset h ?_‚ü©
    ¬∑ rcases comp_mem_uniformity_sets h with ‚ü®t, ht, hts‚ü©
      refine mem_of_superset ht fun (y, z) hyz hyx => @hts (x, z) ‚ü®y, ?_, hyz‚ü© rfl
      exact Quotient.exact hyx.symm _ ht
    ¬∑ exact fun y hy hyx => hy <| congr_arg _ hyx
#align uniform_space.separation_setoid.uniform_space UniformSpace.separationSetoid.uniformSpace

theorem uniformity_quotient :
    ùì§ (Quotient (separationSetoid Œ±)) = (ùì§ Œ±).map fun p : Œ± √ó Œ± => (‚ü¶p.1‚üß, ‚ü¶p.2‚üß) :=
  rfl
#align uniform_space.uniformity_quotient UniformSpace.uniformity_quotient

theorem uniformContinuous_quotient_mk' :
    UniformContinuous (Quotient.mk' : Œ± ‚Üí Quotient (separationSetoid Œ±)) :=
  le_rfl
#align uniform_space.uniform_continuous_quotient_mk UniformSpace.uniformContinuous_quotient_mk'

theorem uniformContinuous_quotient_mk : UniformContinuous (Quotient.mk (separationSetoid Œ±)) :=
  le_rfl

theorem uniformContinuous_quotient {f : Quotient (separationSetoid Œ±) ‚Üí Œ≤}
    (hf : UniformContinuous fun x => f ‚ü¶x‚üß) : UniformContinuous f :=
  hf
#align uniform_space.uniform_continuous_quotient UniformSpace.uniformContinuous_quotient

theorem uniformContinuous_quotient_lift {f : Œ± ‚Üí Œ≤} {h : ‚àÄ a b, (a, b) ‚àà ùì¢ Œ± ‚Üí f a = f b}
    (hf : UniformContinuous f) : UniformContinuous fun a => Quotient.lift f h a :=
  uniformContinuous_quotient hf
#align uniform_space.uniform_continuous_quotient_lift UniformSpace.uniformContinuous_quotient_lift

theorem uniformContinuous_quotient_lift‚ÇÇ {f : Œ± ‚Üí Œ≤ ‚Üí Œ≥}
    {h : ‚àÄ a c b d, (a, b) ‚àà ùì¢ Œ± ‚Üí (c, d) ‚àà ùì¢ Œ≤ ‚Üí f a c = f b d}
    (hf : UniformContinuous fun p : Œ± √ó Œ≤ => f p.1 p.2) :
    UniformContinuous fun p : _ √ó _ => Quotient.lift‚ÇÇ f h p.1 p.2 := by
  rw [UniformContinuous, uniformity_prod_eq_prod, uniformity_quotient, uniformity_quotient,
    Filter.prod_map_map_eq, Filter.tendsto_map'_iff, Filter.tendsto_map'_iff]
  rwa [UniformContinuous, uniformity_prod_eq_prod, Filter.tendsto_map'_iff] at hf
#align uniform_space.uniform_continuous_quotient_lift‚ÇÇ UniformSpace.uniformContinuous_quotient_lift‚ÇÇ

theorem comap_quotient_le_uniformity :
    ((ùì§ <| Quotient <| separationSetoid Œ±).comap fun p : Œ± √ó Œ± => (‚ü¶p.fst‚üß, ‚ü¶p.snd‚üß)) ‚â§ ùì§ Œ± :=
  ((((ùì§ Œ±).basis_sets.map _).comap _).le_basis_iff uniformity_hasBasis_open).2 fun U hU =>
    ‚ü®U, hU.1, fun ‚ü®x, y‚ü© ‚ü®‚ü®x', y'‚ü©, hx', h‚ü© => by
      simp only [Prod.ext_iff, Quotient.eq] at h
      exact (((separationRel_iff_inseparable.1 h.1).prod
        (separationRel_iff_inseparable.1 h.2)).mem_open_iff hU.2).1 hx'‚ü©
#align uniform_space.comap_quotient_le_uniformity UniformSpace.comap_quotient_le_uniformity

theorem comap_quotient_eq_uniformity :
    ((ùì§ <| Quotient <| separationSetoid Œ±).comap fun p : Œ± √ó Œ± => (‚ü¶p.fst‚üß, ‚ü¶p.snd‚üß)) = ùì§ Œ± :=
  le_antisymm comap_quotient_le_uniformity le_comap_map
#align uniform_space.comap_quotient_eq_uniformity UniformSpace.comap_quotient_eq_uniformity

instance separated_separation : SeparatedSpace (Quotient (separationSetoid Œ±)) :=
  ‚ü®Set.ext fun ‚ü®a, b‚ü© =>
      Quotient.inductionOn‚ÇÇ a b fun a b =>
        ‚ü®fun h =>
          have : a ‚âà b := fun s hs =>
            have :
              s ‚àà (ùì§ <| Quotient <| separationSetoid Œ±).comap fun p : Œ± √ó Œ± => (‚ü¶p.1‚üß, ‚ü¶p.2‚üß) :=
              comap_quotient_le_uniformity hs
            let ‚ü®t, ht, hts‚ü© := this
            hts (by dsimp [preimage]; exact h t ht)
          show ‚ü¶a‚üß = ‚ü¶b‚üß from Quotient.sound this,
          fun heq : ‚ü¶a‚üß = ‚ü¶b‚üß => fun h hs => heq ‚ñ∏ refl_mem_uniformity hs‚ü©‚ü©
#align uniform_space.separated_separation UniformSpace.separated_separation

theorem separated_of_uniformContinuous {f : Œ± ‚Üí Œ≤} {x y : Œ±} (H : UniformContinuous f) (h : x ‚âà y) :
    f x ‚âà f y := fun _ h' => h _ (H h')
#align uniform_space.separated_of_uniform_continuous UniformSpace.separated_of_uniformContinuous

theorem eq_of_separated_of_uniformContinuous [SeparatedSpace Œ≤] {f : Œ± ‚Üí Œ≤} {x y : Œ±}
    (H : UniformContinuous f) (h : x ‚âà y) : f x = f y :=
  separated_def.1 (by infer_instance) _ _ <| separated_of_uniformContinuous H h
#align uniform_space.eq_of_separated_of_uniform_continuous UniformSpace.eq_of_separated_of_uniformContinuous

/-- The maximal separated quotient of a uniform space `Œ±`. -/
def SeparationQuotient (Œ± : Type*) [UniformSpace Œ±] :=
  Quotient (separationSetoid Œ±)
#align uniform_space.separation_quotient UniformSpace.SeparationQuotient

namespace SeparationQuotient

instance : UniformSpace (SeparationQuotient Œ±) :=
  separationSetoid.uniformSpace

instance : SeparatedSpace (SeparationQuotient Œ±) :=
  UniformSpace.separated_separation

instance [Inhabited Œ±] : Inhabited (SeparationQuotient Œ±) :=
  inferInstanceAs (Inhabited (Quotient (separationSetoid Œ±)))

lemma mk_eq_mk {x y : Œ±} : (‚ü¶x‚üß : SeparationQuotient Œ±) = ‚ü¶y‚üß ‚Üî Inseparable x y :=
  Quotient.eq'.trans separationRel_iff_inseparable
#align uniform_space.separation_quotient.mk_eq_mk UniformSpace.SeparationQuotient.mk_eq_mk

/-- Factoring functions to a separated space through the separation quotient. -/
def lift [SeparatedSpace Œ≤] (f : Œ± ‚Üí Œ≤) : SeparationQuotient Œ± ‚Üí Œ≤ :=
  if h : UniformContinuous f then Quotient.lift f fun _ _ => eq_of_separated_of_uniformContinuous h
  else fun x => f (Nonempty.some ‚ü®x.out‚ü©)
#align uniform_space.separation_quotient.lift UniformSpace.SeparationQuotient.lift

theorem lift_mk [SeparatedSpace Œ≤] {f : Œ± ‚Üí Œ≤} (h : UniformContinuous f) (a : Œ±) :
    lift f ‚ü¶a‚üß = f a := by rw [lift, dif_pos h]; rfl
#align uniform_space.separation_quotient.lift_mk UniformSpace.SeparationQuotient.lift_mk

theorem uniformContinuous_lift [SeparatedSpace Œ≤] (f : Œ± ‚Üí Œ≤) : UniformContinuous (lift f) := by
  by_cases hf : UniformContinuous f
  ¬∑ rw [lift, dif_pos hf]
    exact uniformContinuous_quotient_lift hf
  ¬∑ rw [lift, dif_neg hf]
    exact uniformContinuous_of_const fun a _ => rfl
#align uniform_space.separation_quotient.uniform_continuous_lift UniformSpace.SeparationQuotient.uniformContinuous_lift

/-- The separation quotient functor acting on functions. -/
def map (f : Œ± ‚Üí Œ≤) : SeparationQuotient Œ± ‚Üí SeparationQuotient Œ≤ :=
  lift (Quotient.mk' ‚àò f)
#align uniform_space.separation_quotient.map UniformSpace.SeparationQuotient.map

theorem map_mk {f : Œ± ‚Üí Œ≤} (h : UniformContinuous f) (a : Œ±) : map f ‚ü¶a‚üß = ‚ü¶f a‚üß := by
  rw [map, lift_mk (uniformContinuous_quotient_mk'.comp h)]; rfl
#align uniform_space.separation_quotient.map_mk UniformSpace.SeparationQuotient.map_mk

theorem uniformContinuous_map (f : Œ± ‚Üí Œ≤) : UniformContinuous (map f) :=
  uniformContinuous_lift (Quotient.mk' ‚àò f)
#align uniform_space.separation_quotient.uniform_continuous_map UniformSpace.SeparationQuotient.uniformContinuous_map

theorem map_unique {f : Œ± ‚Üí Œ≤} (hf : UniformContinuous f)
    {g : SeparationQuotient Œ± ‚Üí SeparationQuotient Œ≤}
    (comm : Quotient.mk _ ‚àò f = g ‚àò Quotient.mk _) : map f = g := by
  ext ‚ü®a‚ü©
  calc
    map f ‚ü¶a‚üß = ‚ü¶f a‚üß := map_mk hf a
    _ = g ‚ü¶a‚üß := congr_fun comm a
#align uniform_space.separation_quotient.map_unique UniformSpace.SeparationQuotient.map_unique

theorem map_id : map (@id Œ±) = id :=
  map_unique uniformContinuous_id rfl
#align uniform_space.separation_quotient.map_id UniformSpace.SeparationQuotient.map_id

theorem map_comp {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ≥} (hf : UniformContinuous f) (hg : UniformContinuous g) :
    map g ‚àò map f = map (g ‚àò f) :=
  (map_unique (hg.comp hf) <| by simp only [Function.comp, map_mk, hf, hg]).symm
#align uniform_space.separation_quotient.map_comp UniformSpace.SeparationQuotient.map_comp

end SeparationQuotient

theorem separation_prod {a‚ÇÅ a‚ÇÇ : Œ±} {b‚ÇÅ b‚ÇÇ : Œ≤} : (a‚ÇÅ, b‚ÇÅ) ‚âà (a‚ÇÇ, b‚ÇÇ) ‚Üî a‚ÇÅ ‚âà a‚ÇÇ ‚àß b‚ÇÅ ‚âà b‚ÇÇ := by
  constructor
  ¬∑ intro h
    exact
      ‚ü®separated_of_uniformContinuous uniformContinuous_fst h,
        separated_of_uniformContinuous uniformContinuous_snd h‚ü©
  ¬∑ rintro ‚ü®eqv_Œ±, eqv_Œ≤‚ü© r r_in
    rw [uniformity_prod] at r_in
    rcases r_in with ‚ü®t_Œ±, ‚ü®r_Œ±, r_Œ±_in, h_Œ±‚ü©, t_Œ≤, ‚ü®r_Œ≤, r_Œ≤_in, h_Œ≤‚ü©, rfl‚ü©
    let p_Œ± := fun p : (Œ± √ó Œ≤) √ó Œ± √ó Œ≤ => (p.1.1, p.2.1)
    let p_Œ≤ := fun p : (Œ± √ó Œ≤) √ó Œ± √ó Œ≤ => (p.1.2, p.2.2)
    have key_Œ± : p_Œ± ((a‚ÇÅ, b‚ÇÅ), (a‚ÇÇ, b‚ÇÇ)) ‚àà r_Œ± := by simp [eqv_Œ± r_Œ± r_Œ±_in]
    have key_Œ≤ : p_Œ≤ ((a‚ÇÅ, b‚ÇÅ), (a‚ÇÇ, b‚ÇÇ)) ‚àà r_Œ≤ := by simp [eqv_Œ≤ r_Œ≤ r_Œ≤_in]
    exact ‚ü®h_Œ± key_Œ±, h_Œ≤ key_Œ≤‚ü©
#align uniform_space.separation_prod UniformSpace.separation_prod

instance Separated.prod [SeparatedSpace Œ±] [SeparatedSpace Œ≤] : SeparatedSpace (Œ± √ó Œ≤) :=
  separated_def.2 fun _ _ H =>
    Prod.ext (eq_of_separated_of_uniformContinuous uniformContinuous_fst H)
      (eq_of_separated_of_uniformContinuous uniformContinuous_snd H)
#align uniform_space.separated.prod UniformSpace.Separated.prod

end UniformSpace
