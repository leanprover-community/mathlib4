/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Patrick Massot
-/
import Mathlib.Tactic.ApplyFun
import Mathlib.Topology.UniformSpace.Basic
import Mathlib.Topology.Separation

#align_import topology.uniform_space.separation from "leanprover-community/mathlib"@"0c1f285a9f6e608ae2bdffa3f993eafb01eba829"

/-!
# Hausdorff properties of uniform spaces. Separation quotient.

This file studies uniform spaces whose underlying topological spaces are separated
(also known as Hausdorff or Tâ‚‚).
This turns out to be equivalent to asking that the intersection of all entourages
is the diagonal only. This condition actually implies the stronger separation property
that the space is Tâ‚ƒ, hence those conditions are equivalent for topologies coming from
a uniform structure.

More generally, the intersection `ğ“¢ X` of all entourages of `X`, which has type `Set (X Ã— X)` is an
equivalence relation on `X`. Points which are equivalent under the relation are basically
undistinguishable from the point of view of the uniform structure. For instance any uniformly
continuous function will send equivalent points to the same value.

The quotient `SeparationQuotient X` of `X` by `ğ“¢ X` has a natural uniform structure which is
separated, and satisfies a universal property: every uniformly continuous function
from `X` to a separated uniform space uniquely factors through `SeparationQuotient X`.
As usual, this allows to turn `SeparationQuotient` into a functor (but we don't use the
category theory library in this file).

These notions admit relative versions, one can ask that `s : Set X` is separated, this
is equivalent to asking that the uniform structure induced on `s` is separated.

## Main definitions

* `separationRel X : Set (X Ã— X)`: the separation relation
* `SeparatedSpace X`: a predicate class asserting that `X` is separated
* `SeparationQuotient X`: the maximal separated quotient of `X`.
* `SeparationQuotient.lift f`: factors a map `f : X â†’ Y` through the separation quotient of `X`.
* `SeparationQuotient.map f`: turns a map `f : X â†’ Y` into a map between the separation quotients
  of `X` and `Y`.

## Main results

* `separated_iff_t2`: the equivalence between being separated and being Hausdorff for uniform
  spaces.
* `SeparationQuotient.uniformContinuous_lift`: factoring a uniformly continuous map through the
  separation quotient gives a uniformly continuous map.
* `SeparationQuotient.uniformContinuous_map`: maps induced between separation quotients are
  uniformly continuous.

##Â Notations

Localized in `uniformity`, we have the notation `ğ“¢ X` for the separation relation
on a uniform space `X`,

## Implementation notes

The separation setoid `separationSetoid` is not declared as a global instance.
It is made a local instance while building the theory of `SeparationQuotient`.
The factored map `SeparationQuotient.lift f` is defined without imposing any condition on
`f`, but returns junk if `f` is not uniformly continuous (constant junk hence it is always
uniformly continuous).

-/

open Filter Set Function Topology Uniformity UniformSpace
open scoped Classical

noncomputable section

universe u v w

variable {Î± : Type u} {Î² : Type v} {Î³ : Type w}
variable [UniformSpace Î±] [UniformSpace Î²] [UniformSpace Î³]

/-!
### Separated uniform spaces
-/

instance (priority := 100) UniformSpace.to_regularSpace : RegularSpace Î± :=
  RegularSpace.ofBasis
    (fun a => by
      rw [nhds_eq_comap_uniformity]
      exact uniformity_hasBasis_closed.comap _)
    fun a V hV => by exact hV.2.preimage <| continuous_const.prod_mk continuous_id

-- porting note: todo: use `Inseparable`
/-- The separation relation is the intersection of all entourages.
  Two points which are related by the separation relation are "indistinguishable"
  according to the uniform structure. -/
def separationRel (Î± : Type u) [UniformSpace Î±] := (ğ“¤ Î±).ker

@[inherit_doc]
scoped[Uniformity] notation "ğ“¢" => separationRel

theorem separated_equiv : Equivalence fun x y => (x, y) âˆˆ ğ“¢ Î± :=
  âŸ¨fun _ _ => refl_mem_uniformity, fun h _s hs => h _ (symm_le_uniformity hs),
    fun {x y z} (hxy : (x, y) âˆˆ ğ“¢ Î±) (hyz : (y, z) âˆˆ ğ“¢ Î±) s (hs : s âˆˆ ğ“¤ Î±) =>
    let âŸ¨t, ht, (h_ts : compRel t t âŠ† s)âŸ© := comp_mem_uniformity_sets hs
    h_ts <| show (x, z) âˆˆ compRel t t from âŸ¨y, hxy t ht, hyz t htâŸ©âŸ©

theorem Filter.HasBasis.mem_separationRel {Î¹ : Sort*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set (Î± Ã— Î±)}
    (h : (ğ“¤ Î±).HasBasis p s) {a : Î± Ã— Î±} : a âˆˆ ğ“¢ Î± â†” âˆ€ i, p i â†’ a âˆˆ s i :=
  h.forall_mem_mem

theorem separationRel_iff_specializes {a b : Î±} : (a, b) âˆˆ ğ“¢ Î± â†” a â¤³ b := by
  simp only [(ğ“¤ Î±).basis_sets.mem_separationRel, id, mem_setOf_eq,
    (nhds_basis_uniformity (ğ“¤ Î±).basis_sets).specializes_iff]

theorem separationRel_iff_inseparable {a b : Î±} : (a, b) âˆˆ ğ“¢ Î± â†” Inseparable a b :=
  separationRel_iff_specializes.trans specializes_iff_inseparable

/-- A uniform space is separated if its separation relation is trivial (each point
is related only to itself). -/
class SeparatedSpace (Î± : Type u) [UniformSpace Î±] : Prop where
  /-- The separation relation is equal to the diagonal `idRel`. -/
  out : ğ“¢ Î± = idRel

theorem separatedSpace_iff {Î± : Type u} [UniformSpace Î±] : SeparatedSpace Î± â†” ğ“¢ Î± = idRel :=
  âŸ¨fun h => h.1, fun h => âŸ¨hâŸ©âŸ©

theorem separated_def {Î± : Type u} [UniformSpace Î±] :
    SeparatedSpace Î± â†” âˆ€ x y, (âˆ€ r âˆˆ ğ“¤ Î±, (x, y) âˆˆ r) â†’ x = y := by
  simp only [separatedSpace_iff, Set.ext_iff, Prod.forall, mem_idRel, separationRel, mem_sInter]
  exact forallâ‚‚_congr fun _ _ => âŸ¨Iff.mp, fun h => âŸ¨h, fun H U hU => H â–¸ refl_mem_uniformity hUâŸ©âŸ©

theorem separated_def' {Î± : Type u} [UniformSpace Î±] :
    SeparatedSpace Î± â†” âˆ€ x y, x â‰  y â†’ âˆƒ r âˆˆ ğ“¤ Î±, (x, y) âˆ‰ r :=
  separated_def.trans <| forallâ‚‚_congr fun x y => by rw [â† not_imp_not]; simp [not_forall]

theorem eq_of_uniformity {Î± : Type*} [UniformSpace Î±] [SeparatedSpace Î±] {x y : Î±}
    (h : âˆ€ {V}, V âˆˆ ğ“¤ Î± â†’ (x, y) âˆˆ V) : x = y :=
  separated_def.mp â€¹SeparatedSpace Î±â€º x y fun _ => h

theorem eq_of_uniformity_basis {Î± : Type*} [UniformSpace Î±] [SeparatedSpace Î±] {Î¹ : Type*}
    {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set (Î± Ã— Î±)} (hs : (ğ“¤ Î±).HasBasis p s) {x y : Î±}
    (h : âˆ€ {i}, p i â†’ (x, y) âˆˆ s i) : x = y :=
  eq_of_uniformity fun V_in => let âŸ¨_, hi, HâŸ© := hs.mem_iff.mp V_in; H (h hi)

theorem eq_of_forall_symmetric {Î± : Type*} [UniformSpace Î±] [SeparatedSpace Î±] {x y : Î±}
    (h : âˆ€ {V}, V âˆˆ ğ“¤ Î± â†’ SymmetricRel V â†’ (x, y) âˆˆ V) : x = y :=
  eq_of_uniformity_basis hasBasis_symmetric (by simpa [and_imp])

theorem eq_of_clusterPt_uniformity [SeparatedSpace Î±] {x y : Î±} (h : ClusterPt (x, y) (ğ“¤ Î±)) :
    x = y :=
  eq_of_uniformity_basis uniformity_hasBasis_closed fun âŸ¨hV, hVcâŸ© =>
    isClosed_iff_clusterPt.1 hVc _ <| h.mono <| le_principal_iff.2 hV

theorem idRel_sub_separationRel (Î± : Type*) [UniformSpace Î±] : idRel âŠ† ğ“¢ Î± := by
  unfold separationRel
  rw [idRel_subset]
  intro x
  suffices âˆ€ t âˆˆ ğ“¤ Î±, (x, x) âˆˆ t by simpa only [refl_mem_uniformity]
  exact fun t => refl_mem_uniformity

theorem separationRel_comap {f : Î± â†’ Î²}
    (h : â€¹UniformSpace Î±â€º = UniformSpace.comap f â€¹UniformSpace Î²â€º) :
    ğ“¢ Î± = Prod.map f f â»Â¹' ğ“¢ Î² := by
  subst h
  dsimp [separationRel]
  simp_rw [uniformity_comap, ((ğ“¤ Î²).comap_hasBasis $ Prod.map f f).ker, ker_def, preimage_iInter]

protected theorem Filter.HasBasis.separationRel {Î¹ : Sort*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set (Î± Ã— Î±)}
    (h : HasBasis (ğ“¤ Î±) p s) : ğ“¢ Î± = â‹‚ (i) (_ : p i), s i := by
  unfold separationRel
  rw [h.ker]

theorem separationRel_eq_inter_closure : ğ“¢ Î± = â‹‚â‚€ (closure '' (ğ“¤ Î±).sets) := by
  simp [uniformity_hasBasis_closure.separationRel]

theorem isClosed_separationRel : IsClosed (ğ“¢ Î±) := by
  rw [separationRel_eq_inter_closure]
  apply isClosed_sInter
  rintro _ âŸ¨t, -, rflâŸ©
  exact isClosed_closure

theorem separated_iff_t2 : SeparatedSpace Î± â†” T2Space Î± := by
  constructor <;> intro h
  Â· rw [t2_iff_isClosed_diagonal, â† show ğ“¢ Î± = diagonal Î± from h.1]
    exact isClosed_separationRel
  Â· rw [separated_def']
    intro x y hxy
    rcases t2_separation hxy with âŸ¨u, v, uo, -, hx, hy, hâŸ©
    rcases isOpen_iff_ball_subset.1 uo x hx with âŸ¨r, hrU, hrâŸ©
    exact âŸ¨r, hrU, fun H => h.le_bot âŸ¨hr H, hyâŸ©âŸ©

-- see Note [lower instance priority]
instance (priority := 100) separated_t3 [SeparatedSpace Î±] : T3Space Î± :=
  haveI := separated_iff_t2.mp â€¹_â€º
  âŸ¨âŸ©

instance Subtype.separatedSpace [SeparatedSpace Î±] (s : Set Î±) : SeparatedSpace s :=
  separated_iff_t2.mpr inferInstance

theorem isClosed_of_spaced_out [SeparatedSpace Î±] {Vâ‚€ : Set (Î± Ã— Î±)} (Vâ‚€_in : Vâ‚€ âˆˆ ğ“¤ Î±) {s : Set Î±}
    (hs : s.Pairwise fun x y => (x, y) âˆ‰ Vâ‚€) : IsClosed s := by
  rcases comp_symm_mem_uniformity_sets Vâ‚€_in with âŸ¨Vâ‚, Vâ‚_in, Vâ‚_symm, h_compâŸ©
  apply isClosed_of_closure_subset
  intro x hx
  rw [mem_closure_iff_ball] at hx
  rcases hx Vâ‚_in with âŸ¨y, hy, hy'âŸ©
  suffices x = y by rwa [this]
  apply eq_of_forall_symmetric
  intro V V_in _
  rcases hx (inter_mem Vâ‚_in V_in) with âŸ¨z, hz, hz'âŸ©
  obtain rfl : z = y := by
    by_contra hzy
    exact hs hz' hy' hzy (h_comp <| mem_comp_of_mem_ball Vâ‚_symm (ball_inter_left x _ _ hz) hy)
  exact ball_inter_right x _ _ hz

theorem isClosed_range_of_spaced_out {Î¹} [SeparatedSpace Î±] {Vâ‚€ : Set (Î± Ã— Î±)} (Vâ‚€_in : Vâ‚€ âˆˆ ğ“¤ Î±)
    {f : Î¹ â†’ Î±} (hf : Pairwise fun x y => (f x, f y) âˆ‰ Vâ‚€) : IsClosed (range f) :=
  isClosed_of_spaced_out Vâ‚€_in <| by
    rintro _ âŸ¨x, rflâŸ© _ âŸ¨y, rflâŸ© h
    exact hf (ne_of_apply_ne f h)

/-!
### Separation quotient
-/

namespace UniformSpace

/-- The separation relation of a uniform space seen as a setoid. -/
def separationSetoid (Î± : Type u) [UniformSpace Î±] : Setoid Î± :=
  âŸ¨fun x y => (x, y) âˆˆ ğ“¢ Î±, separated_equivâŸ©

attribute [local instance] separationSetoid

instance separationSetoid.uniformSpace {Î± : Type u} [UniformSpace Î±] :
    UniformSpace (Quotient (separationSetoid Î±)) where
  toTopologicalSpace := instTopologicalSpaceQuotient
  uniformity := map (fun p : Î± Ã— Î± => (âŸ¦p.1âŸ§, âŸ¦p.2âŸ§)) (ğ“¤ Î±)
  refl := le_trans (by simp [Quotient.exists_rep]) (Filter.map_mono refl_le_uniformity)
  symm := tendsto_map' <| tendsto_map.comp tendsto_swap_uniformity
  comp s hs := by
    rcases comp_open_symm_mem_uniformity_sets hs with âŸ¨U, hU, hUo, -, hUsâŸ©
    refine' mem_of_superset (mem_lift' <| image_mem_map hU) ?_
    simp only [subset_def, Prod.forall, mem_compRel, mem_image, Prod.ext_iff]
    rintro _ _ âŸ¨_, âŸ¨âŸ¨x, yâŸ©, hxyU, rfl, rflâŸ©, âŸ¨âŸ¨y', zâŸ©, hyzU, hy, rflâŸ©âŸ©
    have : y' â¤³ y := separationRel_iff_specializes.1 (Quotient.exact hy)
    exact @hUs (x, z) âŸ¨y', this.mem_open (UniformSpace.isOpen_ball _ hUo) hxyU, hyzUâŸ©
  isOpen_uniformity s := isOpen_coinduced.trans <| by
    simp only [_root_.isOpen_uniformity, Quotient.forall, mem_map', mem_setOf_eq]
    refine forallâ‚‚_congr fun x _ => âŸ¨fun h => ?_, fun h => mem_of_superset h ?_âŸ©
    Â· rcases comp_mem_uniformity_sets h with âŸ¨t, ht, htsâŸ©
      refine mem_of_superset ht fun (y, z) hyz hyx => @hts (x, z) âŸ¨y, ?_, hyzâŸ© rfl
      exact Quotient.exact hyx.symm _ ht
    Â· exact fun y hy hyx => hy <| congr_arg _ hyx

theorem uniformity_quotient :
    ğ“¤ (Quotient (separationSetoid Î±)) = (ğ“¤ Î±).map fun p : Î± Ã— Î± => (âŸ¦p.1âŸ§, âŸ¦p.2âŸ§) :=
  rfl

theorem uniformContinuous_quotient_mk' :
    UniformContinuous (Quotient.mk' : Î± â†’ Quotient (separationSetoid Î±)) :=
  le_rfl

theorem uniformContinuous_quotient_mk : UniformContinuous (Quotient.mk (separationSetoid Î±)) :=
  le_rfl

theorem uniformContinuous_quotient {f : Quotient (separationSetoid Î±) â†’ Î²}
    (hf : UniformContinuous fun x => f âŸ¦xâŸ§) : UniformContinuous f :=
  hf

theorem uniformContinuous_quotient_lift {f : Î± â†’ Î²} {h : âˆ€ a b, (a, b) âˆˆ ğ“¢ Î± â†’ f a = f b}
    (hf : UniformContinuous f) : UniformContinuous fun a => Quotient.lift f h a :=
  uniformContinuous_quotient hf

theorem uniformContinuous_quotient_liftâ‚‚ {f : Î± â†’ Î² â†’ Î³}
    {h : âˆ€ a c b d, (a, b) âˆˆ ğ“¢ Î± â†’ (c, d) âˆˆ ğ“¢ Î² â†’ f a c = f b d}
    (hf : UniformContinuous fun p : Î± Ã— Î² => f p.1 p.2) :
    UniformContinuous fun p : _ Ã— _ => Quotient.liftâ‚‚ f h p.1 p.2 := by
  rw [UniformContinuous, uniformity_prod_eq_prod, uniformity_quotient, uniformity_quotient,
    Filter.prod_map_map_eq, Filter.tendsto_map'_iff, Filter.tendsto_map'_iff]
  rwa [UniformContinuous, uniformity_prod_eq_prod, Filter.tendsto_map'_iff] at hf

theorem comap_quotient_le_uniformity :
    ((ğ“¤ <| Quotient <| separationSetoid Î±).comap fun p : Î± Ã— Î± => (âŸ¦p.fstâŸ§, âŸ¦p.sndâŸ§)) â‰¤ ğ“¤ Î± :=
  ((((ğ“¤ Î±).basis_sets.map _).comap _).le_basis_iff uniformity_hasBasis_open).2 fun U hU =>
    âŸ¨U, hU.1, fun âŸ¨x, yâŸ© âŸ¨âŸ¨x', y'âŸ©, hx', hâŸ© => by
      simp only [Prod.ext_iff, Quotient.eq] at h
      exact (((separationRel_iff_inseparable.1 h.1).prod
        (separationRel_iff_inseparable.1 h.2)).mem_open_iff hU.2).1 hx'âŸ©

theorem comap_quotient_eq_uniformity :
    ((ğ“¤ <| Quotient <| separationSetoid Î±).comap fun p : Î± Ã— Î± => (âŸ¦p.fstâŸ§, âŸ¦p.sndâŸ§)) = ğ“¤ Î± :=
  le_antisymm comap_quotient_le_uniformity le_comap_map

instance separated_separation : SeparatedSpace (Quotient (separationSetoid Î±)) :=
  âŸ¨Set.ext fun âŸ¨a, bâŸ© =>
      Quotient.inductionOnâ‚‚ a b fun a b =>
        âŸ¨fun h =>
          have : a â‰ˆ b := fun s hs =>
            have :
              s âˆˆ (ğ“¤ <| Quotient <| separationSetoid Î±).comap fun p : Î± Ã— Î± => (âŸ¦p.1âŸ§, âŸ¦p.2âŸ§) :=
              comap_quotient_le_uniformity hs
            let âŸ¨t, ht, htsâŸ© := this
            hts (by dsimp [preimage]; exact h t ht)
          show âŸ¦aâŸ§ = âŸ¦bâŸ§ from Quotient.sound this,
          fun heq : âŸ¦aâŸ§ = âŸ¦bâŸ§ => fun h hs => heq â–¸ refl_mem_uniformity hsâŸ©âŸ©

theorem separated_of_uniformContinuous {f : Î± â†’ Î²} {x y : Î±} (H : UniformContinuous f) (h : x â‰ˆ y) :
    f x â‰ˆ f y := fun _ h' => h _ (H h')

theorem eq_of_separated_of_uniformContinuous [SeparatedSpace Î²] {f : Î± â†’ Î²} {x y : Î±}
    (H : UniformContinuous f) (h : x â‰ˆ y) : f x = f y :=
  separated_def.1 (by infer_instance) _ _ <| separated_of_uniformContinuous H h

/-- The maximal separated quotient of a uniform space `Î±`. -/
def SeparationQuotient (Î± : Type*) [UniformSpace Î±] :=
  Quotient (separationSetoid Î±)

namespace SeparationQuotient

instance : UniformSpace (SeparationQuotient Î±) :=
  separationSetoid.uniformSpace

instance : SeparatedSpace (SeparationQuotient Î±) :=
  UniformSpace.separated_separation

instance [Inhabited Î±] : Inhabited (SeparationQuotient Î±) :=
  inferInstanceAs (Inhabited (Quotient (separationSetoid Î±)))

lemma mk_eq_mk {x y : Î±} : (âŸ¦xâŸ§ : SeparationQuotient Î±) = âŸ¦yâŸ§ â†” Inseparable x y :=
  Quotient.eq'.trans separationRel_iff_inseparable

/-- Factoring functions to a separated space through the separation quotient. -/
def lift [SeparatedSpace Î²] (f : Î± â†’ Î²) : SeparationQuotient Î± â†’ Î² :=
  if h : UniformContinuous f then Quotient.lift f fun _ _ => eq_of_separated_of_uniformContinuous h
  else fun x => f (Nonempty.some âŸ¨x.outâŸ©)

theorem lift_mk [SeparatedSpace Î²] {f : Î± â†’ Î²} (h : UniformContinuous f) (a : Î±) :
    lift f âŸ¦aâŸ§ = f a := by rw [lift, dif_pos h]; rfl

theorem uniformContinuous_lift [SeparatedSpace Î²] (f : Î± â†’ Î²) : UniformContinuous (lift f) := by
  by_cases hf : UniformContinuous f
  Â· rw [lift, dif_pos hf]
    exact uniformContinuous_quotient_lift hf
  Â· rw [lift, dif_neg hf]
    exact uniformContinuous_of_const fun a _ => rfl

/-- The separation quotient functor acting on functions. -/
def map (f : Î± â†’ Î²) : SeparationQuotient Î± â†’ SeparationQuotient Î² :=
  lift (Quotient.mk' âˆ˜ f)

theorem map_mk {f : Î± â†’ Î²} (h : UniformContinuous f) (a : Î±) : map f âŸ¦aâŸ§ = âŸ¦f aâŸ§ := by
  rw [map, lift_mk (uniformContinuous_quotient_mk'.comp h)]; rfl

theorem uniformContinuous_map (f : Î± â†’ Î²) : UniformContinuous (map f) :=
  uniformContinuous_lift (Quotient.mk' âˆ˜ f)

theorem map_unique {f : Î± â†’ Î²} (hf : UniformContinuous f)
    {g : SeparationQuotient Î± â†’ SeparationQuotient Î²}
    (comm : Quotient.mk _ âˆ˜ f = g âˆ˜ Quotient.mk _) : map f = g := by
  ext âŸ¨aâŸ©
  calc
    map f âŸ¦aâŸ§ = âŸ¦f aâŸ§ := map_mk hf a
    _ = g âŸ¦aâŸ§ := congr_fun comm a

theorem map_id : map (@id Î±) = id :=
  map_unique uniformContinuous_id rfl

theorem map_comp {f : Î± â†’ Î²} {g : Î² â†’ Î³} (hf : UniformContinuous f) (hg : UniformContinuous g) :
    map g âˆ˜ map f = map (g âˆ˜ f) :=
  (map_unique (hg.comp hf) <| by simp only [Function.comp, map_mk, hf, hg]).symm

end SeparationQuotient

theorem separation_prod {aâ‚ aâ‚‚ : Î±} {bâ‚ bâ‚‚ : Î²} : (aâ‚, bâ‚) â‰ˆ (aâ‚‚, bâ‚‚) â†” aâ‚ â‰ˆ aâ‚‚ âˆ§ bâ‚ â‰ˆ bâ‚‚ := by
  constructor
  Â· intro h
    exact
      âŸ¨separated_of_uniformContinuous uniformContinuous_fst h,
        separated_of_uniformContinuous uniformContinuous_snd hâŸ©
  Â· rintro âŸ¨eqv_Î±, eqv_Î²âŸ© r r_in
    rw [uniformity_prod] at r_in
    rcases r_in with âŸ¨t_Î±, âŸ¨r_Î±, r_Î±_in, h_Î±âŸ©, t_Î², âŸ¨r_Î², r_Î²_in, h_Î²âŸ©, rflâŸ©
    let p_Î± := fun p : (Î± Ã— Î²) Ã— Î± Ã— Î² => (p.1.1, p.2.1)
    let p_Î² := fun p : (Î± Ã— Î²) Ã— Î± Ã— Î² => (p.1.2, p.2.2)
    have key_Î± : p_Î± ((aâ‚, bâ‚), (aâ‚‚, bâ‚‚)) âˆˆ r_Î± := by simp [eqv_Î± r_Î± r_Î±_in]
    have key_Î² : p_Î² ((aâ‚, bâ‚), (aâ‚‚, bâ‚‚)) âˆˆ r_Î² := by simp [eqv_Î² r_Î² r_Î²_in]
    exact âŸ¨h_Î± key_Î±, h_Î² key_Î²âŸ©

instance Separated.prod [SeparatedSpace Î±] [SeparatedSpace Î²] : SeparatedSpace (Î± Ã— Î²) :=
  separated_def.2 fun _ _ H =>
    Prod.ext (eq_of_separated_of_uniformContinuous uniformContinuous_fst H)
      (eq_of_separated_of_uniformContinuous uniformContinuous_snd H)

end UniformSpace
