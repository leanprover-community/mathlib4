/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Patrick Massot, Yury Kudryashov
-/
module

public import Mathlib.Topology.Separation.Regular
public import Mathlib.Topology.UniformSpace.Basic

/-!
# Hausdorff properties of uniform spaces. Separation quotient.

Two points of a topological space are called `Inseparable`,
if their neighborhoods filter are equal.
Equivalently, `Inseparable x y` means that any open set that contains `x` must contain `y`
and vice versa.

In a uniform space, points `x` and `y` are inseparable
if and only if `(x, y)` belongs to all entourages,
see `inseparable_iff_ker_uniformity`.

A uniform space is a regular topological space,
hence separation axioms `T0Space`, `T1Space`, `T2Space`, and `T3Space`
are equivalent for uniform spaces,
and Lean typeclass search can automatically convert from one assumption to another.
We say that a uniform space is *separated*, if it satisfies these axioms.
If you need an `Iff` statement (e.g., to rewrite),
then see `R1Space.t0Space_iff_t2Space` and `RegularSpace.t0Space_iff_t3Space`.

In this file we prove several facts
that relate `Inseparable` and `Specializes` to the uniformity filter.
Most of them are simple corollaries of `Filter.HasBasis.inseparable_iff_uniformity`
for different filter bases of `ğ“¤ Î±`.

Then we study the Kolmogorov quotient `SeparationQuotient X` of a uniform space.
For a general topological space,
this quotient is defined as the quotient by `Inseparable` equivalence relation.
It is the maximal Tâ‚€ quotient of a topological space.

In case of a uniform space, we equip this quotient with a `UniformSpace` structure
that agrees with the quotient topology.
We also prove that the quotient map induces uniformity on the original space.

Finally, we turn `SeparationQuotient` into a functor
(not in terms of `CategoryTheory.Functor` to avoid extra imports)
by defining `SeparationQuotient.lift'` and `SeparationQuotient.map` operations.

## Main definitions

* `SeparationQuotient.instUniformSpace`: uniform space structure on `SeparationQuotient Î±`,
  where `Î±` is a uniform space;

* `SeparationQuotient.lift'`: given a map `f : Î± â†’ Î²`
  from a uniform space to a separated uniform space,
  lift it to a map `SeparationQuotient Î± â†’ Î²`;
  if the original map is not uniformly continuous, then returns a constant map.

* `SeparationQuotient.map`: given a map `f : Î± â†’ Î²` between uniform spaces,
  returns a map `SeparationQuotient Î± â†’ SeparationQuotient Î²`.
  If the original map is not uniformly continuous, then returns a constant map.
  Otherwise, `SeparationQuotient.map f (SeparationQuotient.mk x) = SeparationQuotient.mk (f x)`.

## Main results

* `SeparationQuotient.uniformity_eq`: the uniformity filter on `SeparationQuotient Î±`
  is the push forward of the uniformity filter on `Î±`.
* `SeparationQuotient.comap_mk_uniformity`: the quotient map `Î± â†’ SeparationQuotient Î±`
  induces uniform space structure on the original space.
* `SeparationQuotient.uniformContinuous_lift'`: factoring a uniformly continuous map through the
  separation quotient gives a uniformly continuous map.
* `SeparationQuotient.uniformContinuous_map`: maps induced between separation quotients are
  uniformly continuous.

## Implementation notes

This file used to contain definitions of `separationRel Î±` and `UniformSpace.SeparationQuotient Î±`.
These definitions were equal (but not definitionally equal)
to `{x : Î± Ã— Î± | Inseparable x.1 x.2}` and `SeparationQuotient Î±`, respectively,
and were added to the library before their generalizations to topological spaces.

In https://github.com/leanprover-community/mathlib4/pull/10644, we migrated from these definitions
to more general `Inseparable` and `SeparationQuotient`.

## TODO

Definitions `SeparationQuotient.lift'` and `SeparationQuotient.map`
rely on `UniformSpace` structures in the domain and in the codomain.
We should generalize them to topological spaces.
This generalization will drop `UniformContinuous` assumptions in some lemmas,
and add these assumptions in other lemmas,
so it was not done in https://github.com/leanprover-community/mathlib4/pull/10644 to keep it reasonably sized.

## Keywords

uniform space, separated space, Hausdorff space, separation quotient
-/

@[expose] public section

open Filter Set Function Topology Uniformity UniformSpace

noncomputable section

universe u v w

variable {Î± : Type u} {Î² : Type v} {Î³ : Type w}
variable [UniformSpace Î±] [UniformSpace Î²] [UniformSpace Î³]

/-!
### Separated uniform spaces
-/

instance (priority := 100) UniformSpace.to_regularSpace : RegularSpace Î± :=
  .of_hasBasis
    (fun _ â†¦ nhds_basis_uniformity' uniformity_hasBasis_closed)
    fun a _V hV â†¦ isClosed_ball a hV.2

/--
If the uniformity has a linearly ordered basis, then the space is completely normal.
-/
theorem UniformSpace.completelyNormalSpace_of_hasAntitoneBasis {Î¹ : Type*} [LinearOrder Î¹]
    {B : Î¹ â†’ SetRel Î± Î±} (hB : (uniformity Î±).HasAntitoneBasis B) : CompletelyNormalSpace Î± where
  completely_normal s t hSt hsT := by
    let S (b : Bool) : Set Î± := b.casesOn (false := s) (true := t)
    have hx (b : Bool) (x : S b) : âˆƒ i, Disjoint (ball x.1 ((B i).comp (B i).inv)) (S (!b)) := by
      have hST : Disjoint (S b) (closure (S !b)) := b.casesOn (false := hsT) (true := hSt.symm)
      rw [â† disjoint_nhdsSet_principal, disjoint_principal_right] at hST
      obtain âŸ¨U, hUu, hUâŸ© := UniformSpace.mem_nhds_iff.1 (nhds_le_nhdsSet x.2 hST)
      obtain âŸ¨(V : SetRel Î± Î±), hV, hVs, hVUâŸ© := comp_symm_mem_uniformity_sets hUu
      obtain âŸ¨i, hiâŸ© := hB.mem_iff.1 hV
      refine âŸ¨i, subset_compl_iff_disjoint_right.1 (subset_trans (ball_mono ?_ x.1) hU)âŸ©
      exact subset_trans (SetRel.comp_subset_comp hi (V.inv_eq_self â–¸ (SetRel.inv_mono hi))) hVU
    choose U hU using hx
    have hUS (b : Bool) : â‹ƒ x, ball x.1 (B (U b x)) âˆˆ nhdsSet (S b) := by
      rw [mem_nhdsSet_iff_forall]
      intro x hx
      apply mem_of_superset (ball_mem_nhds x (hB.mem (U b âŸ¨x, hxâŸ©)))
      exact subset_iUnion (fun x => ball x.1 (B (U b x))) âŸ¨x, hxâŸ©
    rw [Filter.disjoint_iff]
    refine âŸ¨_, hUS false, _, hUS true, ?_âŸ©
    have hdj (b : Bool) (x : S b) (y : S (!b)) (hxy : U b x â‰¤ U (!b) y) :
        Disjoint (ball x.1 (B (U b x))) (ball y.1 (B (U (!b) y))) := by
      rw [Set.disjoint_iff]
      intro z hz
      exact (hU b x).notMem_of_mem_left (mem_ball_comp hz.1 (hB.antitone hxy hz.2)) y.2
    simp_rw [disjoint_iUnion_left, disjoint_iUnion_right]
    intro x y
    exact (le_total (U false x) (U true y)).elim
      (fun h => hdj false x y h) (fun h => (hdj true y x h).symm)

instance (priority := 100) UniformSpace.completelyNormalSpace_of_isCountablyGenerated_uniformity
    [(uniformity Î±).IsCountablyGenerated] : CompletelyNormalSpace Î± :=
  (has_seq_basis Î±).elim fun _ hB =>
    UniformSpace.completelyNormalSpace_of_hasAntitoneBasis hB.1

theorem Filter.HasBasis.specializes_iff_uniformity {Î¹ : Sort*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set (Î± Ã— Î±)}
    (h : (ğ“¤ Î±).HasBasis p s) {x y : Î±} : x â¤³ y â†” âˆ€ i, p i â†’ (x, y) âˆˆ s i :=
  (nhds_basis_uniformity h).specializes_iff

theorem Filter.HasBasis.inseparable_iff_uniformity {Î¹ : Sort*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set (Î± Ã— Î±)}
    (h : (ğ“¤ Î±).HasBasis p s) {x y : Î±} : Inseparable x y â†” âˆ€ i, p i â†’ (x, y) âˆˆ s i :=
  specializes_iff_inseparable.symm.trans h.specializes_iff_uniformity

theorem inseparable_iff_ker_uniformity {x y : Î±} : Inseparable x y â†” (x, y) âˆˆ (ğ“¤ Î±).ker :=
  (ğ“¤ Î±).basis_sets.inseparable_iff_uniformity

protected theorem Inseparable.nhds_le_uniformity {x y : Î±} (h : Inseparable x y) :
    ğ“ (x, y) â‰¤ ğ“¤ Î± := by
  rw [h.prod rfl]
  apply nhds_le_uniformity

theorem inseparable_iff_clusterPt_uniformity {x y : Î±} :
    Inseparable x y â†” ClusterPt (x, y) (ğ“¤ Î±) := by
  refine âŸ¨fun h â†¦ .of_nhds_le h.nhds_le_uniformity, fun h â†¦ ?_âŸ©
  simp_rw [uniformity_hasBasis_closed.inseparable_iff_uniformity, isClosed_iff_clusterPt]
  exact fun U âŸ¨hU, hUcâŸ© â†¦ hUc _ <| h.mono <| le_principal_iff.2 hU

theorem t0Space_iff_uniformity :
    T0Space Î± â†” âˆ€ x y, (âˆ€ r âˆˆ ğ“¤ Î±, (x, y) âˆˆ r) â†’ x = y := by
  simp only [t0Space_iff_inseparable, inseparable_iff_ker_uniformity, mem_ker]

theorem t0Space_iff_uniformity' :
    T0Space Î± â†” Pairwise fun x y â†¦ âˆƒ r âˆˆ ğ“¤ Î±, (x, y) âˆ‰ r := by
  simp [t0Space_iff_not_inseparable, inseparable_iff_ker_uniformity]

theorem t0Space_iff_ker_uniformity : T0Space Î± â†” (ğ“¤ Î±).ker = diagonal Î± := by
  simp_rw [t0Space_iff_uniformity, subset_antisymm_iff, diagonal_subset_iff, subset_def,
    Prod.forall, Filter.mem_ker, mem_diagonal_iff, iff_self_and]
  exact fun _ x s hs â†¦ refl_mem_uniformity hs

theorem eq_of_uniformity {Î± : Type*} [UniformSpace Î±] [T0Space Î±] {x y : Î±}
    (h : âˆ€ {V}, V âˆˆ ğ“¤ Î± â†’ (x, y) âˆˆ V) : x = y :=
  t0Space_iff_uniformity.mp â€¹T0Space Î±â€º x y @h

theorem eq_of_uniformity_basis {Î± : Type*} [UniformSpace Î±] [T0Space Î±] {Î¹ : Sort*}
    {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set (Î± Ã— Î±)} (hs : (ğ“¤ Î±).HasBasis p s) {x y : Î±}
    (h : âˆ€ {i}, p i â†’ (x, y) âˆˆ s i) : x = y :=
  (hs.inseparable_iff_uniformity.2 @h).eq

theorem eq_of_forall_symmetric {Î± : Type*} [UniformSpace Î±] [T0Space Î±] {x y : Î±}
    (h : âˆ€ {V}, V âˆˆ ğ“¤ Î± â†’ SetRel.IsSymm V â†’ (x, y) âˆˆ V) : x = y :=
  eq_of_uniformity_basis hasBasis_symmetric (by simpa)

theorem eq_of_clusterPt_uniformity [T0Space Î±] {x y : Î±} (h : ClusterPt (x, y) (ğ“¤ Î±)) : x = y :=
  (inseparable_iff_clusterPt_uniformity.2 h).eq

theorem Filter.Tendsto.inseparable_iff_uniformity {Î²} {l : Filter Î²} [NeBot l] {f g : Î² â†’ Î±}
    {a b : Î±} (ha : Tendsto f l (ğ“ a)) (hb : Tendsto g l (ğ“ b)) :
    Inseparable a b â†” Tendsto (fun x â†¦ (f x, g x)) l (ğ“¤ Î±) := by
  refine âŸ¨fun h â†¦ (ha.prodMk_nhds hb).mono_right h.nhds_le_uniformity, fun h â†¦ ?_âŸ©
  rw [inseparable_iff_clusterPt_uniformity]
  exact (ClusterPt.of_le_nhds (ha.prodMk_nhds hb)).mono h

theorem isClosed_of_spaced_out [T0Space Î±] {Vâ‚€ : Set (Î± Ã— Î±)} (Vâ‚€_in : Vâ‚€ âˆˆ ğ“¤ Î±) {s : Set Î±}
    (hs : s.Pairwise fun x y => (x, y) âˆ‰ Vâ‚€) : IsClosed s := by
  rcases comp_symm_mem_uniformity_sets Vâ‚€_in with âŸ¨Vâ‚, Vâ‚_in, Vâ‚_symm, h_compâŸ©
  apply isClosed_of_closure_subset
  intro x hx
  rw [mem_closure_iff_ball] at hx
  rcases hx Vâ‚_in with âŸ¨y, hy, hy'âŸ©
  suffices x = y by rwa [this]
  apply eq_of_forall_symmetric
  intro V V_in _
  rcases hx (inter_mem Vâ‚_in V_in) with âŸ¨z, hz, hz'âŸ©
  obtain rfl : z = y := by
    by_contra hzy
    exact hs hz' hy' hzy (h_comp <| mem_comp_of_mem_ball (ball_inter_left x _ _ hz) hy)
  exact ball_inter_right x _ _ hz

theorem isClosed_range_of_spaced_out {Î¹} [T0Space Î±] {Vâ‚€ : Set (Î± Ã— Î±)} (Vâ‚€_in : Vâ‚€ âˆˆ ğ“¤ Î±)
    {f : Î¹ â†’ Î±} (hf : Pairwise fun x y => (f x, f y) âˆ‰ Vâ‚€) : IsClosed (range f) :=
  isClosed_of_spaced_out Vâ‚€_in <| by
    rintro _ âŸ¨x, rflâŸ© _ âŸ¨y, rflâŸ© h
    exact hf (ne_of_apply_ne f h)

/-!
### Separation quotient
-/

namespace SeparationQuotient

theorem comap_map_mk_uniformity : comap (Prod.map mk mk) (map (Prod.map mk mk) (ğ“¤ Î±)) = ğ“¤ Î± := by
  refine le_antisymm ?_ le_comap_map
  refine ((((ğ“¤ Î±).basis_sets.map _).comap _).le_basis_iff uniformity_hasBasis_open).2 fun U hU â†¦ ?_
  refine âŸ¨U, hU.1, fun (xâ‚, xâ‚‚) âŸ¨(yâ‚, yâ‚‚), hyU, hxyâŸ© â†¦ ?_âŸ©
  simp only [Prod.map, Prod.ext_iff, mk_eq_mk] at hxy
  exact ((hxy.1.prod hxy.2).mem_open_iff hU.2).1 hyU

instance instUniformSpace : UniformSpace (SeparationQuotient Î±) where
  uniformity := map (Prod.map mk mk) (ğ“¤ Î±)
  symm := tendsto_map' <| tendsto_map.comp tendsto_swap_uniformity
  comp := fun t ht â†¦ by
    rcases comp_open_symm_mem_uniformity_sets ht with âŸ¨U, hU, hUo, -, hUtâŸ©
    refine mem_of_superset (mem_lift' <| image_mem_map hU) ?_
    simp only [subset_def, Prod.forall, SetRel.mem_comp, mem_image, Prod.ext_iff]
    rintro _ _ âŸ¨_, âŸ¨âŸ¨x, yâŸ©, hxyU, rfl, rflâŸ©, âŸ¨âŸ¨y', zâŸ©, hyzU, hy, rflâŸ©âŸ©
    have : y' â¤³ y := (mk_eq_mk.1 hy).specializes
    exact @hUt (x, z) âŸ¨y', this.mem_open (UniformSpace.isOpen_ball _ hUo) hxyU, hyzUâŸ©
  nhds_eq_comap_uniformity := surjective_mk.forall.2 fun x â†¦ comap_injective surjective_mk <| by
    conv_lhs => rw [comap_mk_nhds_mk, nhds_eq_comap_uniformity, â† comap_map_mk_uniformity]
    simp only [Filter.comap_comap, Function.comp_def, Prod.map_apply]

theorem uniformity_eq : ğ“¤ (SeparationQuotient Î±) = (ğ“¤ Î±).map (Prod.map mk mk) := rfl

theorem uniformContinuous_mk : UniformContinuous (mk : Î± â†’ SeparationQuotient Î±) :=
  le_rfl

theorem uniformContinuous_dom {f : SeparationQuotient Î± â†’ Î²} :
    UniformContinuous f â†” UniformContinuous (f âˆ˜ mk) :=
  .rfl

theorem uniformContinuous_domâ‚‚ {f : SeparationQuotient Î± Ã— SeparationQuotient Î² â†’ Î³} :
    UniformContinuous f â†” UniformContinuous fun p : Î± Ã— Î² â†¦ f (mk p.1, mk p.2) := by
  simp only [UniformContinuous, uniformity_prod_eq_prod, uniformity_eq, prod_map_map_eq,
    tendsto_map'_iff]
  rfl

theorem uniformContinuous_lift {f : Î± â†’ Î²} (h : âˆ€ a b, Inseparable a b â†’ f a = f b) :
    UniformContinuous (lift f h) â†” UniformContinuous f :=
  .rfl

theorem uniformContinuous_uncurry_liftâ‚‚ {f : Î± â†’ Î² â†’ Î³}
    (h : âˆ€ a c b d, Inseparable a b â†’ Inseparable c d â†’ f a c = f b d) :
    UniformContinuous (uncurry <| liftâ‚‚ f h) â†” UniformContinuous (uncurry f) :=
  uniformContinuous_domâ‚‚

theorem comap_mk_uniformity : (ğ“¤ (SeparationQuotient Î±)).comap (Prod.map mk mk) = ğ“¤ Î± :=
  comap_map_mk_uniformity

open Classical in
/-- Factoring functions to a separated space through the separation quotient.

TODO: unify with `SeparationQuotient.lift`. -/
def lift' [T0Space Î²] (f : Î± â†’ Î²) : SeparationQuotient Î± â†’ Î² :=
  if hc : UniformContinuous f then lift f fun _ _ h => (h.map hc.continuous).eq
  else fun x => f (Nonempty.some âŸ¨x.outâŸ©)

theorem lift'_mk [T0Space Î²] {f : Î± â†’ Î²} (h : UniformContinuous f) (a : Î±) :
    lift' f (mk a) = f a := by rw [lift', dif_pos h, lift_mk]

theorem uniformContinuous_lift' [T0Space Î²] (f : Î± â†’ Î²) : UniformContinuous (lift' f) := by
  by_cases hf : UniformContinuous f
  Â· rwa [lift', dif_pos hf, uniformContinuous_lift]
  Â· rw [lift', dif_neg hf]
    exact uniformContinuous_of_const fun a _ => rfl

/-- The separation quotient functor acting on functions. -/
def map (f : Î± â†’ Î²) : SeparationQuotient Î± â†’ SeparationQuotient Î² := lift' (mk âˆ˜ f)

theorem map_mk {f : Î± â†’ Î²} (h : UniformContinuous f) (a : Î±) : map f (mk a) = mk (f a) := by
  rw [map, lift'_mk (uniformContinuous_mk.comp h)]; rfl

theorem uniformContinuous_map (f : Î± â†’ Î²) : UniformContinuous (map f) :=
  uniformContinuous_lift' _

theorem map_unique {f : Î± â†’ Î²} (hf : UniformContinuous f)
    {g : SeparationQuotient Î± â†’ SeparationQuotient Î²} (comm : mk âˆ˜ f = g âˆ˜ mk) : map f = g := by
  ext âŸ¨aâŸ©
  calc
    map f âŸ¦aâŸ§ = âŸ¦f aâŸ§ := map_mk hf a
    _ = g âŸ¦aâŸ§ := congr_fun comm a

@[simp]
theorem map_id : map (@id Î±) = id := map_unique uniformContinuous_id rfl

theorem map_comp {f : Î± â†’ Î²} {g : Î² â†’ Î³} (hf : UniformContinuous f) (hg : UniformContinuous g) :
    map g âˆ˜ map f = map (g âˆ˜ f) :=
  (map_unique (hg.comp hf) <| by simp only [Function.comp_def, map_mk, hf, hg]).symm

end SeparationQuotient
