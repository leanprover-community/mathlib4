/-
Copyright (c) 2019 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
import Mathlib.Topology.UniformSpace.Cauchy
import Mathlib.Topology.UniformSpace.Separation

#align_import topology.uniform_space.pi from "leanprover-community/mathlib"@"2705404e701abc6b3127da906f40bae062a169c9"

/-!
# Indexed product of uniform spaces
-/


noncomputable section

open Uniformity Topology

section

open Filter UniformSpace Function

universe u

variable {Œπ Œπ' Œ≤ : Type*} (Œ± : Œπ ‚Üí Type u) [U : ‚àÄ i, UniformSpace (Œ± i)] [UniformSpace Œ≤]

instance Pi.uniformSpace : UniformSpace (‚àÄ i, Œ± i) :=
  UniformSpace.ofCoreEq (‚®Ö i, UniformSpace.comap (fun a : ‚àÄ i, Œ± i => a i) (U i)).toCore
      Pi.topologicalSpace <|
    Eq.symm toTopologicalSpace_iInf
#align Pi.uniform_space Pi.uniformSpace

lemma Pi.uniformSpace_eq :
    Pi.uniformSpace Œ± = ‚®Ö i, UniformSpace.comap (eval i) (U i) := by
  ext : 1; rfl

theorem Pi.uniformity :
    ùì§ (‚àÄ i, Œ± i) = ‚®Ö i : Œπ, (Filter.comap fun a => (a.1 i, a.2 i)) (ùì§ (Œ± i)) :=
  iInf_uniformity
#align Pi.uniformity Pi.uniformity

variable {Œ±}

theorem uniformContinuous_pi {Œ≤ : Type*} [UniformSpace Œ≤] {f : Œ≤ ‚Üí ‚àÄ i, Œ± i} :
    UniformContinuous f ‚Üî ‚àÄ i, UniformContinuous fun x => f x i := by
  -- porting note: required `Function.comp` to close
  simp only [UniformContinuous, Pi.uniformity, tendsto_iInf, tendsto_comap_iff, Function.comp]
#align uniform_continuous_pi uniformContinuous_pi

variable (Œ±)

theorem Pi.uniformContinuous_proj (i : Œπ) : UniformContinuous fun a : ‚àÄ i : Œπ, Œ± i => a i :=
  uniformContinuous_pi.1 uniformContinuous_id i
#align Pi.uniform_continuous_proj Pi.uniformContinuous_proj

theorem Pi.uniformContinuous_precomp' {Œπ' : Type*} (œÜ : Œπ' ‚Üí Œπ) :
    UniformContinuous (fun (f : (‚àÄ i, Œ± i)) (j : Œπ') ‚Ü¶ f (œÜ j)) :=
  uniformContinuous_pi.mpr fun j ‚Ü¶ uniformContinuous_proj Œ± (œÜ j)

theorem Pi.uniformContinuous_precomp {Œπ' Œ≤ : Type*} [UniformSpace Œ≤] (œÜ : Œπ' ‚Üí Œπ) :
    UniformContinuous (¬∑ ‚àò œÜ : (Œπ ‚Üí Œ≤) ‚Üí (Œπ' ‚Üí Œ≤)) :=
  Pi.uniformContinuous_precomp' _ œÜ

theorem Pi.uniformContinuous_postcomp' {Œ≤ : Œπ ‚Üí Type*} [‚àÄ i, UniformSpace (Œ≤ i)]
    {g : ‚àÄ i, Œ± i ‚Üí Œ≤ i} (hg : ‚àÄ i, UniformContinuous (g i)) :
    UniformContinuous (fun (f : (‚àÄ i, Œ± i)) (i : Œπ) ‚Ü¶ g i (f i)) :=
  uniformContinuous_pi.mpr fun i ‚Ü¶ (hg i).comp <| uniformContinuous_proj Œ± i

theorem Pi.uniformContinuous_postcomp {Œ± Œ≤ : Type*} [UniformSpace Œ±] [UniformSpace Œ≤] {g : Œ± ‚Üí Œ≤}
    (hg : UniformContinuous g) : UniformContinuous (g ‚àò ¬∑ : (Œπ ‚Üí Œ±) ‚Üí (Œπ ‚Üí Œ≤)) :=
  Pi.uniformContinuous_postcomp' _ fun _ ‚Ü¶ hg

lemma cauchy_pi_iff [Nonempty Œπ] {l : Filter (‚àÄ i, Œ± i)} :
    Cauchy l ‚Üî ‚àÄ i, Cauchy (map (eval i) l) := by
  simp_rw [Pi.uniformSpace_eq, cauchy_iInf_uniformSpace, cauchy_comap_uniformSpace]

lemma cauchy_pi_iff' {l : Filter (‚àÄ i, Œ± i)} [l.NeBot] :
    Cauchy l ‚Üî ‚àÄ i, Cauchy (map (eval i) l) := by
  simp_rw [Pi.uniformSpace_eq, cauchy_iInf_uniformSpace', cauchy_comap_uniformSpace]

lemma Cauchy.pi [Nonempty Œπ] {l : ‚àÄ i, Filter (Œ± i)} (hl : ‚àÄ i, Cauchy (l i)) :
    Cauchy (Filter.pi l) := by
  have := fun i ‚Ü¶ (hl i).1
  simpa [cauchy_pi_iff]

instance Pi.complete [‚àÄ i, CompleteSpace (Œ± i)] : CompleteSpace (‚àÄ i, Œ± i) where
  complete {f} hf := by
    have := hf.1
    simp_rw [cauchy_pi_iff', cauchy_iff_exists_le_nhds] at hf
    choose x hx using hf
    use x
    rwa [nhds_pi, le_pi]
#align Pi.complete Pi.complete

instance Pi.separated [‚àÄ i, SeparatedSpace (Œ± i)] : SeparatedSpace (‚àÄ i, Œ± i) :=
  separated_def.2 fun x y H => by
    ext i
    -- porting note: should be `eq_ofSeparated_ofUniformContinuous`?
    apply eq_of_separated_of_uniformContinuous (Pi.uniformContinuous_proj Œ± i)
    apply H
#align Pi.separated Pi.separated

lemma Pi.uniformSpace_comap_precomp' (f : Œπ' ‚Üí Œπ) :
    UniformSpace.comap (fun g i' ‚Ü¶ g (f i')) (Pi.uniformSpace (fun i' ‚Ü¶ Œ± (f i'))) =
    ‚®Ö i', UniformSpace.comap (eval (f i')) (U (f i')) := by
  simp [Pi.uniformSpace_eq, UniformSpace.comap_iInf, ‚Üê UniformSpace.comap_comap, comp]

lemma Pi.uniformSpace_comap_restrict (S : Set Œπ) :
    UniformSpace.comap (S.restrict) (Pi.uniformSpace (fun i : S ‚Ü¶ Œ± i)) =
    ‚®Ö i ‚àà S, UniformSpace.comap (eval i) (U i) := by
  simp [‚Üê iInf_subtype'', ‚Üê uniformSpace_comap_precomp' _ ((‚Üë) : S ‚Üí Œπ), Set.restrict]

lemma Pi.uniformSpace_comap_precomp (f : Œπ' ‚Üí Œπ) :
    UniformSpace.comap (¬∑ ‚àò f) (Pi.uniformSpace (fun _ ‚Ü¶ Œ≤)) =
    ‚®Ö i', UniformSpace.comap (eval (f i')) ‚ÄπUniformSpace Œ≤‚Ä∫ :=
  uniformSpace_comap_precomp' (fun _ ‚Ü¶ Œ≤) f

end
