/-
Copyright (c) 2019 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
import Mathlib.Topology.UniformSpace.UniformEmbedding

#align_import topology.uniform_space.pi from "leanprover-community/mathlib"@"2705404e701abc6b3127da906f40bae062a169c9"

/-!
# Indexed product of uniform spaces
-/


noncomputable section

open scoped Uniformity Topology
open Filter UniformSpace Function Set

universe u

variable {Î¹ Î¹' Î² : Type*} (Î± : Î¹ â†’ Type u) [U : âˆ€ i, UniformSpace (Î± i)] [UniformSpace Î²]

instance Pi.uniformSpace : UniformSpace (âˆ€ i, Î± i) :=
  UniformSpace.ofCoreEq (â¨… i, UniformSpace.comap (eval i) (U i)).toCore
      Pi.topologicalSpace <|
    Eq.symm toTopologicalSpace_iInf
#align Pi.uniform_space Pi.uniformSpace

lemma Pi.uniformSpace_eq :
    Pi.uniformSpace Î± = â¨… i, UniformSpace.comap (eval i) (U i) := by
  ext : 1; rfl

theorem Pi.uniformity :
    ð“¤ (âˆ€ i, Î± i) = â¨… i : Î¹, (Filter.comap fun a => (a.1 i, a.2 i)) (ð“¤ (Î± i)) :=
  iInf_uniformity
#align Pi.uniformity Pi.uniformity

variable {Î±}

instance [Countable Î¹] [âˆ€ i, IsCountablyGenerated (ð“¤ (Î± i))] :
    IsCountablyGenerated (ð“¤ (âˆ€ i, Î± i)) := by
  rw [Pi.uniformity]
  infer_instance

theorem uniformContinuous_pi {Î² : Type*} [UniformSpace Î²] {f : Î² â†’ âˆ€ i, Î± i} :
    UniformContinuous f â†” âˆ€ i, UniformContinuous fun x => f x i := by
  -- Porting note: required `Function.comp` to close
  simp only [UniformContinuous, Pi.uniformity, tendsto_iInf, tendsto_comap_iff, Function.comp]
#align uniform_continuous_pi uniformContinuous_pi

variable (Î±)

theorem Pi.uniformContinuous_proj (i : Î¹) : UniformContinuous fun a : âˆ€ i : Î¹, Î± i => a i :=
  uniformContinuous_pi.1 uniformContinuous_id i
#align Pi.uniform_continuous_proj Pi.uniformContinuous_proj

theorem Pi.uniformContinuous_precomp' (Ï† : Î¹' â†’ Î¹) :
    UniformContinuous (fun (f : (âˆ€ i, Î± i)) (j : Î¹') â†¦ f (Ï† j)) :=
  uniformContinuous_pi.mpr fun j â†¦ uniformContinuous_proj Î± (Ï† j)

theorem Pi.uniformContinuous_precomp (Ï† : Î¹' â†’ Î¹) :
    UniformContinuous (Â· âˆ˜ Ï† : (Î¹ â†’ Î²) â†’ (Î¹' â†’ Î²)) :=
  Pi.uniformContinuous_precomp' _ Ï†

theorem Pi.uniformContinuous_postcomp' {Î² : Î¹ â†’ Type*} [âˆ€ i, UniformSpace (Î² i)]
    {g : âˆ€ i, Î± i â†’ Î² i} (hg : âˆ€ i, UniformContinuous (g i)) :
    UniformContinuous (fun (f : (âˆ€ i, Î± i)) (i : Î¹) â†¦ g i (f i)) :=
  uniformContinuous_pi.mpr fun i â†¦ (hg i).comp <| uniformContinuous_proj Î± i

theorem Pi.uniformContinuous_postcomp {Î± : Type*} [UniformSpace Î±] {g : Î± â†’ Î²}
    (hg : UniformContinuous g) : UniformContinuous (g âˆ˜ Â· : (Î¹ â†’ Î±) â†’ (Î¹ â†’ Î²)) :=
  Pi.uniformContinuous_postcomp' _ fun _ â†¦ hg

lemma Pi.uniformSpace_comap_precomp' (Ï† : Î¹' â†’ Î¹) :
    UniformSpace.comap (fun g i' â†¦ g (Ï† i')) (Pi.uniformSpace (fun i' â†¦ Î± (Ï† i'))) =
    â¨… i', UniformSpace.comap (eval (Ï† i')) (U (Ï† i')) := by
  simp [Pi.uniformSpace_eq, UniformSpace.comap_iInf, â† UniformSpace.comap_comap, comp]

lemma Pi.uniformSpace_comap_precomp (Ï† : Î¹' â†’ Î¹) :
    UniformSpace.comap (Â· âˆ˜ Ï†) (Pi.uniformSpace (fun _ â†¦ Î²)) =
    â¨… i', UniformSpace.comap (eval (Ï† i')) â€¹UniformSpace Î²â€º :=
  uniformSpace_comap_precomp' (fun _ â†¦ Î²) Ï†

lemma Pi.uniformContinuous_restrict (S : Set Î¹) :
    UniformContinuous (S.restrict : (âˆ€ i : Î¹, Î± i) â†’ (âˆ€ i : S, Î± i)) :=
  Pi.uniformContinuous_precomp' _ ((â†‘) : S â†’ Î¹)

lemma Pi.uniformSpace_comap_restrict (S : Set Î¹) :
    UniformSpace.comap (S.restrict) (Pi.uniformSpace (fun i : S â†¦ Î± i)) =
    â¨… i âˆˆ S, UniformSpace.comap (eval i) (U i) := by
  simp (config := { unfoldPartialApp := true })
    [â† iInf_subtype'', â† uniformSpace_comap_precomp' _ ((â†‘) : S â†’ Î¹), Set.restrict]

lemma cauchy_pi_iff [Nonempty Î¹] {l : Filter (âˆ€ i, Î± i)} :
    Cauchy l â†” âˆ€ i, Cauchy (map (eval i) l) := by
  simp_rw [Pi.uniformSpace_eq, cauchy_iInf_uniformSpace, cauchy_comap_uniformSpace]

lemma cauchy_pi_iff' {l : Filter (âˆ€ i, Î± i)} [l.NeBot] :
    Cauchy l â†” âˆ€ i, Cauchy (map (eval i) l) := by
  simp_rw [Pi.uniformSpace_eq, cauchy_iInf_uniformSpace', cauchy_comap_uniformSpace]

lemma Cauchy.pi [Nonempty Î¹] {l : âˆ€ i, Filter (Î± i)} (hl : âˆ€ i, Cauchy (l i)) :
    Cauchy (Filter.pi l) := by
  have := fun i â†¦ (hl i).1
  simpa [cauchy_pi_iff]

instance Pi.complete [âˆ€ i, CompleteSpace (Î± i)] : CompleteSpace (âˆ€ i, Î± i) where
  complete {f} hf := by
    have := hf.1
    simp_rw [cauchy_pi_iff', cauchy_iff_exists_le_nhds] at hf
    choose x hx using hf
    use x
    rwa [nhds_pi, le_pi]
#align Pi.complete Pi.complete

#align Pi.separated Pi.instT0Space

lemma Pi.uniformSpace_comap_restrict_sUnion (ð”– : Set (Set Î¹)) :
    UniformSpace.comap (â‹ƒâ‚€ ð”–).restrict (Pi.uniformSpace (fun i : (â‹ƒâ‚€ ð”–) â†¦ Î± i)) =
    â¨… S âˆˆ ð”–, UniformSpace.comap S.restrict (Pi.uniformSpace (fun i : S â†¦ Î± i)) := by
  simp_rw [Pi.uniformSpace_comap_restrict Î±, iInf_sUnion]

/- An infimum of complete uniformities is complete,
as long as the whole family is bounded by some common T2 topology. -/
protected theorem CompleteSpace.iInf {Î¹ X : Type*} {u : Î¹ â†’ UniformSpace X}
    (hu : âˆ€ i, @CompleteSpace X (u i))
    (ht : âˆƒ t, @T2Space X t âˆ§ âˆ€ i, (u i).toTopologicalSpace â‰¤ t) :
    @CompleteSpace X (â¨… i, u i) := by
  -- We can assume `X` is nonempty.
  nontriviality X
  rcases ht with âŸ¨t, ht, hutâŸ©
  -- The diagonal map `(X, â¨… i, u i) â†’ âˆ€ i, (X, u i)` is a uniform embedding.
  have : @UniformInducing X (Î¹ â†’ X) (â¨… i, u i) (Pi.uniformSpace (U := u)) (const Î¹) := by
    simp_rw [uniformInducing_iff, iInf_uniformity, Pi.uniformity, Filter.comap_iInf,
      Filter.comap_comap, (Â· âˆ˜ Â·), const, Prod.eta, comap_id']
  -- Hence, it suffices to show that its range, the diagonal, is closed in `Î  i, (X, u i)`.
  simp_rw [@completeSpace_iff_isComplete_range _ _ (_) (_) _ this, range_const_eq_diagonal,
    setOf_forall]
  -- The separation of `t` ensures that this is the case in `Î  i, (X, t)`, hence the result
  -- since the topology associated to each `u i` is finer than `t`.
  have : Pi.topologicalSpace (tâ‚‚ := fun i â†¦ (u i).toTopologicalSpace) â‰¤
         Pi.topologicalSpace (tâ‚‚ := fun _ â†¦ t) :=
    iInf_mono fun i â†¦ induced_mono <| hut i
  refine IsClosed.isComplete <| .mono ?_ this
  exact isClosed_iInter fun i â†¦ isClosed_iInter fun j â†¦
    isClosed_eq (continuous_apply _) (continuous_apply _)
