/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Patrick Massot, S√©bastien Gou√´zel, Zhouhang Zhou, Reid Barton,
Anatole Dedecker
-/
import Mathlib.Topology.Homeomorph
import Mathlib.Topology.UniformSpace.UniformEmbedding
import Mathlib.Topology.UniformSpace.Pi

#align_import topology.uniform_space.equiv from "leanprover-community/mathlib"@"4c19a16e4b705bf135cf9a80ac18fcc99c438514"

/-!
# Uniform isomorphisms

This file defines uniform isomorphisms between two uniform spaces. They are bijections with both
directions uniformly continuous. We denote uniform isomorphisms with the notation `‚âÉ·µ§`.

# Main definitions

* `UniformEquiv Œ± Œ≤`: The type of uniform isomorphisms from `Œ±` to `Œ≤`.
  This type can be denoted using the following notation: `Œ± ‚âÉ·µ§ Œ≤`.

-/


open Set Filter

universe u v

variable {Œ± : Type u} {Œ≤ : Type*} {Œ≥ : Type*} {Œ¥ : Type*}

-- not all spaces are homeomorphic to each other
/-- Uniform isomorphism between `Œ±` and `Œ≤` -/
--@[nolint has_nonempty_instance] -- Porting note: missing linter?
structure UniformEquiv (Œ± : Type*) (Œ≤ : Type*) [UniformSpace Œ±] [UniformSpace Œ≤] extends
  Œ± ‚âÉ Œ≤ where
  /-- Uniform continuity of the function -/
  uniformContinuous_toFun : UniformContinuous toFun
  /-- Uniform continuity of the inverse -/
  uniformContinuous_invFun : UniformContinuous invFun
#align uniform_equiv UniformEquiv

/-- Uniform isomorphism between `Œ±` and `Œ≤` -/
infixl:25 " ‚âÉ·µ§ " => UniformEquiv

namespace UniformEquiv

variable [UniformSpace Œ±] [UniformSpace Œ≤] [UniformSpace Œ≥] [UniformSpace Œ¥]

theorem toEquiv_injective : Function.Injective (toEquiv : Œ± ‚âÉ·µ§ Œ≤ ‚Üí Œ± ‚âÉ Œ≤)
  | ‚ü®e, h‚ÇÅ, h‚ÇÇ‚ü©, ‚ü®e', h‚ÇÅ', h‚ÇÇ'‚ü©, h => by simpa only [mk.injEq]
                                         -- üéâ no goals
#align uniform_equiv.to_equiv_injective UniformEquiv.toEquiv_injective

instance : EquivLike (Œ± ‚âÉ·µ§ Œ≤) Œ± Œ≤ where
  coe := fun h => h.toEquiv
  inv := fun h => h.toEquiv.symm
  left_inv := fun h => h.left_inv
  right_inv := fun h => h.right_inv
  coe_injective' := fun _ _ H _ => toEquiv_injective <| FunLike.ext' H

@[simp]
theorem uniformEquiv_mk_coe (a : Equiv Œ± Œ≤) (b c) : (UniformEquiv.mk a b c : Œ± ‚Üí Œ≤) = a :=
  rfl
#align uniform_equiv.uniform_equiv_mk_coe UniformEquiv.uniformEquiv_mk_coe

/-- Inverse of a uniform isomorphism. -/
protected def symm (h : Œ± ‚âÉ·µ§ Œ≤) : Œ≤ ‚âÉ·µ§ Œ±
    where
  uniformContinuous_toFun := h.uniformContinuous_invFun
  uniformContinuous_invFun := h.uniformContinuous_toFun
  toEquiv := h.toEquiv.symm
#align uniform_equiv.symm UniformEquiv.symm

/-- See Note [custom simps projection]. We need to specify this projection explicitly in this case,
  because it is a composition of multiple projections. -/
def Simps.apply (h : Œ± ‚âÉ·µ§ Œ≤) : Œ± ‚Üí Œ≤ :=
  h
#align uniform_equiv.simps.apply UniformEquiv.Simps.apply

/-- See Note [custom simps projection] -/
def Simps.symm_apply (h : Œ± ‚âÉ·µ§ Œ≤) : Œ≤ ‚Üí Œ± :=
  h.symm
#align uniform_equiv.simps.symm_apply UniformEquiv.Simps.symm_apply

initialize_simps_projections UniformEquiv (toFun ‚Üí apply, invFun ‚Üí symm_apply)

@[simp]
theorem coe_toEquiv (h : Œ± ‚âÉ·µ§ Œ≤) : ‚áëh.toEquiv = h :=
  rfl
#align uniform_equiv.coe_to_equiv UniformEquiv.coe_toEquiv

@[simp]
theorem coe_symm_toEquiv (h : Œ± ‚âÉ·µ§ Œ≤) : ‚áëh.toEquiv.symm = h.symm :=
  rfl
#align uniform_equiv.coe_symm_to_equiv UniformEquiv.coe_symm_toEquiv

@[ext]
theorem ext {h h' : Œ± ‚âÉ·µ§ Œ≤} (H : ‚àÄ x, h x = h' x) : h = h' :=
  toEquiv_injective <| Equiv.ext H
#align uniform_equiv.ext UniformEquiv.ext

/-- Identity map as a uniform isomorphism. -/
@[simps! (config := { fullyApplied := false }) apply]
protected def refl (Œ± : Type*) [UniformSpace Œ±] : Œ± ‚âÉ·µ§ Œ±
    where
  uniformContinuous_toFun := uniformContinuous_id
  uniformContinuous_invFun := uniformContinuous_id
  toEquiv := Equiv.refl Œ±
#align uniform_equiv.refl UniformEquiv.refl

/-- Composition of two uniform isomorphisms. -/
protected def trans (h‚ÇÅ : Œ± ‚âÉ·µ§ Œ≤) (h‚ÇÇ : Œ≤ ‚âÉ·µ§ Œ≥) : Œ± ‚âÉ·µ§ Œ≥
    where
  uniformContinuous_toFun := h‚ÇÇ.uniformContinuous_toFun.comp h‚ÇÅ.uniformContinuous_toFun
  uniformContinuous_invFun := h‚ÇÅ.uniformContinuous_invFun.comp h‚ÇÇ.uniformContinuous_invFun
  toEquiv := Equiv.trans h‚ÇÅ.toEquiv h‚ÇÇ.toEquiv
#align uniform_equiv.trans UniformEquiv.trans

@[simp]
theorem trans_apply (h‚ÇÅ : Œ± ‚âÉ·µ§ Œ≤) (h‚ÇÇ : Œ≤ ‚âÉ·µ§ Œ≥) (a : Œ±) : h‚ÇÅ.trans h‚ÇÇ a = h‚ÇÇ (h‚ÇÅ a) :=
  rfl
#align uniform_equiv.trans_apply UniformEquiv.trans_apply

@[simp]
theorem uniformEquiv_mk_coe_symm (a : Equiv Œ± Œ≤) (b c) :
    ((UniformEquiv.mk a b c).symm : Œ≤ ‚Üí Œ±) = a.symm :=
  rfl
#align uniform_equiv.uniform_equiv_mk_coe_symm UniformEquiv.uniformEquiv_mk_coe_symm

@[simp]
theorem refl_symm : (UniformEquiv.refl Œ±).symm = UniformEquiv.refl Œ± :=
  rfl
#align uniform_equiv.refl_symm UniformEquiv.refl_symm

protected theorem uniformContinuous (h : Œ± ‚âÉ·µ§ Œ≤) : UniformContinuous h :=
  h.uniformContinuous_toFun
#align uniform_equiv.uniform_continuous UniformEquiv.uniformContinuous

@[continuity]
protected theorem continuous (h : Œ± ‚âÉ·µ§ Œ≤) : Continuous h :=
  h.uniformContinuous.continuous
#align uniform_equiv.continuous UniformEquiv.continuous

protected theorem uniformContinuous_symm (h : Œ± ‚âÉ·µ§ Œ≤) : UniformContinuous h.symm :=
  h.uniformContinuous_invFun
#align uniform_equiv.uniform_continuous_symm UniformEquiv.uniformContinuous_symm

-- otherwise `by continuity` can't prove continuity of `h.to_equiv.symm`
@[continuity]
protected theorem continuous_symm (h : Œ± ‚âÉ·µ§ Œ≤) : Continuous h.symm :=
  h.uniformContinuous_symm.continuous
#align uniform_equiv.continuous_symm UniformEquiv.continuous_symm

/-- A uniform isomorphism as a homeomorphism. -/
-- @[simps] -- Porting note: removed, `simps?` produced no `simp` lemmas
protected def toHomeomorph (e : Œ± ‚âÉ·µ§ Œ≤) : Œ± ‚âÉ‚Çú Œ≤ :=
  { e.toEquiv with
    continuous_toFun := e.continuous
    continuous_invFun := e.continuous_symm }
#align uniform_equiv.to_homeomorph UniformEquiv.toHomeomorph

lemma toHomeomorph_apply (e : Œ± ‚âÉ·µ§ Œ≤) : (e.toHomeomorph : Œ± ‚Üí Œ≤) = e := rfl
#align uniform_equiv.to_homeomorph_apply UniformEquiv.toHomeomorph_apply

lemma toHomeomorph_symm_apply (e : Œ± ‚âÉ·µ§ Œ≤) : (e.toHomeomorph.symm : Œ≤ ‚Üí Œ±) = e.symm := rfl
#align uniform_equiv.to_homeomorph_symm_apply UniformEquiv.toHomeomorph_symm_apply

@[simp]
theorem apply_symm_apply (h : Œ± ‚âÉ·µ§ Œ≤) (x : Œ≤) : h (h.symm x) = x :=
  h.toEquiv.apply_symm_apply x
#align uniform_equiv.apply_symm_apply UniformEquiv.apply_symm_apply

@[simp]
theorem symm_apply_apply (h : Œ± ‚âÉ·µ§ Œ≤) (x : Œ±) : h.symm (h x) = x :=
  h.toEquiv.symm_apply_apply x
#align uniform_equiv.symm_apply_apply UniformEquiv.symm_apply_apply

protected theorem bijective (h : Œ± ‚âÉ·µ§ Œ≤) : Function.Bijective h :=
  h.toEquiv.bijective
#align uniform_equiv.bijective UniformEquiv.bijective

protected theorem injective (h : Œ± ‚âÉ·µ§ Œ≤) : Function.Injective h :=
  h.toEquiv.injective
#align uniform_equiv.injective UniformEquiv.injective

protected theorem surjective (h : Œ± ‚âÉ·µ§ Œ≤) : Function.Surjective h :=
  h.toEquiv.surjective
#align uniform_equiv.surjective UniformEquiv.surjective

/-- Change the uniform equiv `f` to make the inverse function definitionally equal to `g`. -/
def changeInv (f : Œ± ‚âÉ·µ§ Œ≤) (g : Œ≤ ‚Üí Œ±) (hg : Function.RightInverse g f) : Œ± ‚âÉ·µ§ Œ≤ :=
  have : g = f.symm :=
    funext fun x => calc
      g x = f.symm (f (g x)) := (f.left_inv (g x)).symm
      _ = f.symm x := by rw [hg x]
                         -- üéâ no goals
  { toFun := f
    invFun := g
    left_inv := by convert f.left_inv
                   -- üéâ no goals
    right_inv := by convert f.right_inv using 1
                    -- üéâ no goals
    uniformContinuous_toFun := f.uniformContinuous
    uniformContinuous_invFun := by convert f.symm.uniformContinuous }
                                   -- üéâ no goals
#align uniform_equiv.change_inv UniformEquiv.changeInv

@[simp]
theorem symm_comp_self (h : Œ± ‚âÉ·µ§ Œ≤) : (h.symm : Œ≤ ‚Üí Œ±) ‚àò h = id :=
  funext h.symm_apply_apply
#align uniform_equiv.symm_comp_self UniformEquiv.symm_comp_self

@[simp]
theorem self_comp_symm (h : Œ± ‚âÉ·µ§ Œ≤) : (h : Œ± ‚Üí Œ≤) ‚àò h.symm = id :=
  funext h.apply_symm_apply
#align uniform_equiv.self_comp_symm UniformEquiv.self_comp_symm

-- @[simp] -- Porting note: `simp` can prove this `simp only [Equiv.range_eq_univ]`
theorem range_coe (h : Œ± ‚âÉ·µ§ Œ≤) : range h = univ :=
  h.surjective.range_eq
#align uniform_equiv.range_coe UniformEquiv.range_coe

theorem image_symm (h : Œ± ‚âÉ·µ§ Œ≤) : image h.symm = preimage h :=
  funext h.symm.toEquiv.image_eq_preimage
#align uniform_equiv.image_symm UniformEquiv.image_symm

theorem preimage_symm (h : Œ± ‚âÉ·µ§ Œ≤) : preimage h.symm = image h :=
  (funext h.toEquiv.image_eq_preimage).symm
#align uniform_equiv.preimage_symm UniformEquiv.preimage_symm

-- @[simp] -- Porting note: `simp` can prove this `simp only [Equiv.image_preimage]`
theorem image_preimage (h : Œ± ‚âÉ·µ§ Œ≤) (s : Set Œ≤) : h '' (h ‚Åª¬π' s) = s :=
  h.toEquiv.image_preimage s
#align uniform_equiv.image_preimage UniformEquiv.image_preimage

--@[simp] -- Porting note: `simp` can prove this `simp only [Equiv.preimage_image]`
theorem preimage_image (h : Œ± ‚âÉ·µ§ Œ≤) (s : Set Œ±) : h ‚Åª¬π' (h '' s) = s :=
  h.toEquiv.preimage_image s
#align uniform_equiv.preimage_image UniformEquiv.preimage_image

protected theorem uniformInducing (h : Œ± ‚âÉ·µ§ Œ≤) : UniformInducing h :=
  uniformInducing_of_compose h.uniformContinuous h.symm.uniformContinuous <| by
    simp only [symm_comp_self, uniformInducing_id]
    -- üéâ no goals
#align uniform_equiv.uniform_inducing UniformEquiv.uniformInducing

theorem comap_eq (h : Œ± ‚âÉ·µ§ Œ≤) : UniformSpace.comap h ‚Äπ_‚Ä∫ = ‚Äπ_‚Ä∫ := by
  ext : 1; exact h.uniformInducing.comap_uniformity
  -- ‚ä¢ uniformity Œ± = uniformity Œ±
           -- üéâ no goals
#align uniform_equiv.comap_eq UniformEquiv.comap_eq

protected theorem uniformEmbedding (h : Œ± ‚âÉ·µ§ Œ≤) : UniformEmbedding h :=
  ‚ü®h.uniformInducing, h.injective‚ü©
#align uniform_equiv.uniform_embedding UniformEquiv.uniformEmbedding

/-- Uniform equiv given a uniform embedding. -/
noncomputable def ofUniformEmbedding (f : Œ± ‚Üí Œ≤) (hf : UniformEmbedding f) : Œ± ‚âÉ·µ§ Set.range f
    where
  uniformContinuous_toFun := hf.toUniformInducing.uniformContinuous.subtype_mk _
  uniformContinuous_invFun := by
    rw [hf.toUniformInducing.uniformContinuous_iff, Equiv.invFun_as_coe,
      Equiv.self_comp_ofInjective_symm]
    exact uniformContinuous_subtype_val
    -- üéâ no goals
  toEquiv := Equiv.ofInjective f hf.inj
#align uniform_equiv.of_uniform_embedding UniformEquiv.ofUniformEmbedding

/-- If two sets are equal, then they are uniformly equivalent. -/
def setCongr {s t : Set Œ±} (h : s = t) : s ‚âÉ·µ§ t
    where
  uniformContinuous_toFun := uniformContinuous_subtype_val.subtype_mk _
  uniformContinuous_invFun := uniformContinuous_subtype_val.subtype_mk _
  toEquiv := Equiv.setCongr h
#align uniform_equiv.set_congr UniformEquiv.setCongr

/-- Product of two uniform isomorphisms. -/
def prodCongr (h‚ÇÅ : Œ± ‚âÉ·µ§ Œ≤) (h‚ÇÇ : Œ≥ ‚âÉ·µ§ Œ¥) : Œ± √ó Œ≥ ‚âÉ·µ§ Œ≤ √ó Œ¥
    where
  uniformContinuous_toFun :=
    (h‚ÇÅ.uniformContinuous.comp uniformContinuous_fst).prod_mk
      (h‚ÇÇ.uniformContinuous.comp uniformContinuous_snd)
  uniformContinuous_invFun :=
    (h‚ÇÅ.symm.uniformContinuous.comp uniformContinuous_fst).prod_mk
      (h‚ÇÇ.symm.uniformContinuous.comp uniformContinuous_snd)
  toEquiv := h‚ÇÅ.toEquiv.prodCongr h‚ÇÇ.toEquiv
#align uniform_equiv.prod_congr UniformEquiv.prodCongr

@[simp]
theorem prodCongr_symm (h‚ÇÅ : Œ± ‚âÉ·µ§ Œ≤) (h‚ÇÇ : Œ≥ ‚âÉ·µ§ Œ¥) :
    (h‚ÇÅ.prodCongr h‚ÇÇ).symm = h‚ÇÅ.symm.prodCongr h‚ÇÇ.symm :=
  rfl
#align uniform_equiv.prod_congr_symm UniformEquiv.prodCongr_symm

@[simp]
theorem coe_prodCongr (h‚ÇÅ : Œ± ‚âÉ·µ§ Œ≤) (h‚ÇÇ : Œ≥ ‚âÉ·µ§ Œ¥) : ‚áë(h‚ÇÅ.prodCongr h‚ÇÇ) = Prod.map h‚ÇÅ h‚ÇÇ :=
  rfl
#align uniform_equiv.coe_prod_congr UniformEquiv.coe_prodCongr

section

variable (Œ± Œ≤ Œ≥)

/-- `Œ± √ó Œ≤` is uniformly isomorphic to `Œ≤ √ó Œ±`. -/
def prodComm : Œ± √ó Œ≤ ‚âÉ·µ§ Œ≤ √ó Œ±
    where
  uniformContinuous_toFun := uniformContinuous_snd.prod_mk uniformContinuous_fst
  uniformContinuous_invFun := uniformContinuous_snd.prod_mk uniformContinuous_fst
  toEquiv := Equiv.prodComm Œ± Œ≤
#align uniform_equiv.prod_comm UniformEquiv.prodComm

@[simp]
theorem prodComm_symm : (prodComm Œ± Œ≤).symm = prodComm Œ≤ Œ± :=
  rfl
#align uniform_equiv.prod_comm_symm UniformEquiv.prodComm_symm

@[simp]
theorem coe_prodComm : ‚áë(prodComm Œ± Œ≤) = Prod.swap :=
  rfl
#align uniform_equiv.coe_prod_comm UniformEquiv.coe_prodComm

/-- `(Œ± √ó Œ≤) √ó Œ≥` is uniformly isomorphic to `Œ± √ó (Œ≤ √ó Œ≥)`. -/
def prodAssoc : (Œ± √ó Œ≤) √ó Œ≥ ‚âÉ·µ§ Œ± √ó Œ≤ √ó Œ≥
    where
  uniformContinuous_toFun :=
    (uniformContinuous_fst.comp uniformContinuous_fst).prod_mk
      ((uniformContinuous_snd.comp uniformContinuous_fst).prod_mk uniformContinuous_snd)
  uniformContinuous_invFun := by -- Porting note: the `rw` was not necessary in Lean 3
    rw [Equiv.invFun, Equiv.prodAssoc]
    -- ‚ä¢ UniformContinuous { toFun := fun p => (p.fst.fst, p.fst.snd, p.snd), invFun  ‚Ä¶
    exact (uniformContinuous_fst.prod_mk (uniformContinuous_fst.comp
    uniformContinuous_snd)).prod_mk (uniformContinuous_snd.comp uniformContinuous_snd)
  toEquiv := Equiv.prodAssoc Œ± Œ≤ Œ≥
#align uniform_equiv.prod_assoc UniformEquiv.prodAssoc

/-- `Œ± √ó {*}` is uniformly isomorphic to `Œ±`. -/
@[simps! (config := { fullyApplied := false }) apply]
def prodPunit : Œ± √ó PUnit ‚âÉ·µ§ Œ± where
  toEquiv := Equiv.prodPUnit Œ±
  uniformContinuous_toFun := uniformContinuous_fst
  uniformContinuous_invFun := uniformContinuous_id.prod_mk uniformContinuous_const
#align uniform_equiv.prod_punit UniformEquiv.prodPunit

/-- `{*} √ó Œ±` is uniformly isomorphic to `Œ±`. -/
def punitProd : PUnit √ó Œ± ‚âÉ·µ§ Œ± :=
  (prodComm _ _).trans (prodPunit _)
#align uniform_equiv.punit_prod UniformEquiv.punitProd

@[simp]
theorem coe_punitProd : ‚áë(punitProd Œ±) = Prod.snd :=
  rfl
#align uniform_equiv.coe_punit_prod UniformEquiv.coe_punitProd

/-- Uniform equivalence between `ULift Œ±` and `Œ±`. -/
def ulift : ULift.{v, u} Œ± ‚âÉ·µ§ Œ± :=
  { Equiv.ulift with
    uniformContinuous_toFun := uniformContinuous_comap
    uniformContinuous_invFun := by
      have hf : UniformInducing (@Equiv.ulift.{v, u} Œ±).toFun := ‚ü®rfl‚ü©
      -- ‚ä¢ UniformContinuous { toFun := src‚úù.toFun, invFun := src‚úù.invFun, left_inv :=  ‚Ä¶
      simp_rw [hf.uniformContinuous_iff]
      -- ‚ä¢ UniformContinuous (Equiv.ulift.toFun ‚àò Equiv.ulift.invFun)
      exact uniformContinuous_id }
      -- üéâ no goals
#align uniform_equiv.ulift UniformEquiv.ulift

end

/-- If `Œπ` has a unique element, then `Œπ ‚Üí Œ±` is homeomorphic to `Œ±`. -/
@[simps! (config := { fullyApplied := false })]
def funUnique (Œπ Œ± : Type*) [Unique Œπ] [UniformSpace Œ±] : (Œπ ‚Üí Œ±) ‚âÉ·µ§ Œ±
    where
  toEquiv := Equiv.funUnique Œπ Œ±
  uniformContinuous_toFun := Pi.uniformContinuous_proj _ _
  uniformContinuous_invFun := uniformContinuous_pi.mpr fun _ => uniformContinuous_id
#align uniform_equiv.fun_unique UniformEquiv.funUnique

/-- Uniform isomorphism between dependent functions `Œ† i : Fin 2, Œ± i` and `Œ± 0 √ó Œ± 1`. -/
@[simps! (config := { fullyApplied := false })]
def piFinTwo (Œ± : Fin 2 ‚Üí Type u) [‚àÄ i, UniformSpace (Œ± i)] : (‚àÄ i, Œ± i) ‚âÉ·µ§ Œ± 0 √ó Œ± 1
    where
  toEquiv := piFinTwoEquiv Œ±
  uniformContinuous_toFun := (Pi.uniformContinuous_proj _ 0).prod_mk (Pi.uniformContinuous_proj _ 1)
  uniformContinuous_invFun :=
    uniformContinuous_pi.mpr <| Fin.forall_fin_two.2 ‚ü®uniformContinuous_fst, uniformContinuous_snd‚ü©
#align uniform_equiv.pi_fin_two UniformEquiv.piFinTwo

/-- Uniform isomorphism between `Œ±¬≤ = Fin 2 ‚Üí Œ±` and `Œ± √ó Œ±`. -/
-- Porting note: made `Œ±` explicit
@[simps! (config := { fullyApplied := false })]
def finTwoArrow (Œ± : Type*) [UniformSpace Œ±] : (Fin 2 ‚Üí Œ±) ‚âÉ·µ§ Œ± √ó Œ± :=
  { piFinTwo fun _ => Œ± with toEquiv := finTwoArrowEquiv Œ± }
#align uniform_equiv.fin_two_arrow UniformEquiv.finTwoArrow

/-- A subset of a uniform space is uniformly isomorphic to its image under a uniform isomorphism.
-/
def image (e : Œ± ‚âÉ·µ§ Œ≤) (s : Set Œ±) : s ‚âÉ·µ§ e '' s
    where
  uniformContinuous_toFun := (e.uniformContinuous.comp uniformContinuous_subtype_val).subtype_mk _
  uniformContinuous_invFun :=
    (e.symm.uniformContinuous.comp uniformContinuous_subtype_val).subtype_mk _
  toEquiv := e.toEquiv.image s
#align uniform_equiv.image UniformEquiv.image

end UniformEquiv

/-- A uniform inducing equiv between uniform spaces is a uniform isomorphism. -/
-- @[simps] -- Porting note: removed, `simps?` produced no `simp` lemmas
def Equiv.toUniformEquivOfUniformInducing [UniformSpace Œ±] [UniformSpace Œ≤] (f : Œ± ‚âÉ Œ≤)
    (hf : UniformInducing f) : Œ± ‚âÉ·µ§ Œ≤ :=
  { f with
    uniformContinuous_toFun := hf.uniformContinuous
    uniformContinuous_invFun := hf.uniformContinuous_iff.2 <| by simpa using uniformContinuous_id }
                                                                 -- üéâ no goals
#align equiv.to_uniform_equiv_of_uniform_inducing Equiv.toUniformEquivOfUniformInducing
