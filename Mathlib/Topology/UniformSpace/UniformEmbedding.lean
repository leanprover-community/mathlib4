/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, SÃ©bastien GouÃ«zel, Patrick Massot
-/
import Mathlib.Topology.UniformSpace.Cauchy
import Mathlib.Topology.UniformSpace.Separation
import Mathlib.Topology.DenseEmbedding

#align_import topology.uniform_space.uniform_embedding from "leanprover-community/mathlib"@"195fcd60ff2bfe392543bceb0ec2adcdb472db4c"

/-!
# Uniform embeddings of uniform spaces.

Extension of uniform continuous functions.
-/


open Filter Function Set Uniformity Topology

section

universe u v w
variable {Î± : Type u} {Î² : Type v} {Î³ : Type w} [UniformSpace Î±] [UniformSpace Î²] [UniformSpace Î³]

/-!
### Uniform inducing maps
-/

/-- A map `f : Î± â†’ Î²` between uniform spaces is called *uniform inducing* if the uniformity filter
on `Î±` is the pullback of the uniformity filter on `Î²` under `Prod.map f f`. If `Î±` is a separated
space, then this implies that `f` is injective, hence it is a `UniformEmbedding`. -/
@[mk_iff]
structure UniformInducing (f : Î± â†’ Î²) : Prop where
  /-- The uniformity filter on the domain is the pullback of the uniformity filter on the codomain
  under `Prod.map f f`. -/
  comap_uniformity : comap (fun x : Î± Ã— Î± => (f x.1, f x.2)) (ğ“¤ Î²) = ğ“¤ Î±
#align uniform_inducing UniformInducing
#align uniform_inducing_iff uniformInducing_iff

lemma uniformInducing_iff_uniformSpace {f : Î± â†’ Î²} :
    UniformInducing f â†” â€¹UniformSpace Î²â€º.comap f = â€¹UniformSpace Î±â€º := by
  rw [uniformInducing_iff, UniformSpace.ext_iff, Filter.ext_iff]
  rfl

protected alias âŸ¨UniformInducing.comap_uniformSpace, _âŸ© := uniformInducing_iff_uniformSpace
#align uniform_inducing.comap_uniform_space UniformInducing.comap_uniformSpace

lemma uniformInducing_iff' {f : Î± â†’ Î²} :
    UniformInducing f â†” UniformContinuous f âˆ§ comap (Prod.map f f) (ğ“¤ Î²) â‰¤ ğ“¤ Î± := by
  rw [uniformInducing_iff, UniformContinuous, tendsto_iff_comap, le_antisymm_iff, and_comm]; rfl
#align uniform_inducing_iff' uniformInducing_iff'

protected lemma Filter.HasBasis.uniformInducing_iff {Î¹ Î¹'} {p : Î¹ â†’ Prop} {p' : Î¹' â†’ Prop} {s s'}
    (h : (ğ“¤ Î±).HasBasis p s) (h' : (ğ“¤ Î²).HasBasis p' s') {f : Î± â†’ Î²} :
    UniformInducing f â†”
      (âˆ€ i, p' i â†’ âˆƒ j, p j âˆ§ âˆ€ x y, (x, y) âˆˆ s j â†’ (f x, f y) âˆˆ s' i) âˆ§
        (âˆ€ j, p j â†’ âˆƒ i, p' i âˆ§ âˆ€ x y, (f x, f y) âˆˆ s' i â†’ (x, y) âˆˆ s j) := by
  simp [uniformInducing_iff', h.uniformContinuous_iff h', (h'.comap _).le_basis_iff h, subset_def]
#align filter.has_basis.uniform_inducing_iff Filter.HasBasis.uniformInducing_iff

theorem UniformInducing.mk' {f : Î± â†’ Î²}
    (h : âˆ€ s, s âˆˆ ğ“¤ Î± â†” âˆƒ t âˆˆ ğ“¤ Î², âˆ€ x y : Î±, (f x, f y) âˆˆ t â†’ (x, y) âˆˆ s) : UniformInducing f :=
  âŸ¨by simp [eq_comm, Filter.ext_iff, subset_def, h]âŸ©
#align uniform_inducing.mk' UniformInducing.mk'

theorem uniformInducing_id : UniformInducing (@id Î±) :=
  âŸ¨by rw [â† Prod.map_def, Prod.map_id, comap_id]âŸ©
#align uniform_inducing_id uniformInducing_id

theorem UniformInducing.comp {g : Î² â†’ Î³} (hg : UniformInducing g) {f : Î± â†’ Î²}
    (hf : UniformInducing f) : UniformInducing (g âˆ˜ f) :=
  âŸ¨by rw [â† hf.1, â† hg.1, comap_comap]; rflâŸ©
#align uniform_inducing.comp UniformInducing.comp

theorem UniformInducing.of_comp_iff {g : Î² â†’ Î³} (hg : UniformInducing g) {f : Î± â†’ Î²} :
    UniformInducing (g âˆ˜ f) â†” UniformInducing f := by
  refine âŸ¨fun h â†¦ ?_, hg.compâŸ©
  rw [uniformInducing_iff, â† hg.comap_uniformity, comap_comap, â† h.comap_uniformity,
    Function.comp, Function.comp]

theorem UniformInducing.basis_uniformity {f : Î± â†’ Î²} (hf : UniformInducing f) {Î¹ : Sort*}
    {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set (Î² Ã— Î²)} (H : (ğ“¤ Î²).HasBasis p s) :
    (ğ“¤ Î±).HasBasis p fun i => Prod.map f f â»Â¹' s i :=
  hf.1 â–¸ H.comap _
#align uniform_inducing.basis_uniformity UniformInducing.basis_uniformity

theorem UniformInducing.cauchy_map_iff {f : Î± â†’ Î²} (hf : UniformInducing f) {F : Filter Î±} :
    Cauchy (map f F) â†” Cauchy F := by
  simp only [Cauchy, map_neBot_iff, prod_map_map_eq, map_le_iff_le_comap, â† hf.comap_uniformity]
#align uniform_inducing.cauchy_map_iff UniformInducing.cauchy_map_iff

theorem uniformInducing_of_compose {f : Î± â†’ Î²} {g : Î² â†’ Î³} (hf : UniformContinuous f)
    (hg : UniformContinuous g) (hgf : UniformInducing (g âˆ˜ f)) : UniformInducing f := by
  refine âŸ¨le_antisymm ?_ hf.le_comapâŸ©
  rw [â† hgf.1, â† Prod.map_def, â† Prod.map_def, â† Prod.map_comp_map f f g g, â† comap_comap]
  exact comap_mono hg.le_comap
#align uniform_inducing_of_compose uniformInducing_of_compose

theorem UniformInducing.uniformContinuous {f : Î± â†’ Î²} (hf : UniformInducing f) :
    UniformContinuous f := (uniformInducing_iff'.1 hf).1
#align uniform_inducing.uniform_continuous UniformInducing.uniformContinuous

theorem UniformInducing.uniformContinuous_iff {f : Î± â†’ Î²} {g : Î² â†’ Î³} (hg : UniformInducing g) :
    UniformContinuous f â†” UniformContinuous (g âˆ˜ f) := by
  dsimp only [UniformContinuous, Tendsto]
  rw [â† hg.comap_uniformity, â† map_le_iff_le_comap, Filter.map_map]; rfl
#align uniform_inducing.uniform_continuous_iff UniformInducing.uniformContinuous_iff

theorem UniformInducing.uniformContinuousOn_iff {f : Î± â†’ Î²} {g : Î² â†’ Î³} {S : Set Î±}
    (hg : UniformInducing g) :
    UniformContinuousOn f S â†” UniformContinuousOn (g âˆ˜ f) S := by
  dsimp only [UniformContinuousOn, Tendsto]
  rw [â† hg.comap_uniformity, â† map_le_iff_le_comap, Filter.map_map, comp_def, comp_def]

theorem UniformInducing.inducing {f : Î± â†’ Î²} (h : UniformInducing f) : Inducing f := by
  obtain rfl := h.comap_uniformSpace
  exact inducing_induced f
#align uniform_inducing.inducing UniformInducing.inducing

theorem UniformInducing.prod {Î±' : Type*} {Î²' : Type*} [UniformSpace Î±'] [UniformSpace Î²']
    {eâ‚ : Î± â†’ Î±'} {eâ‚‚ : Î² â†’ Î²'} (hâ‚ : UniformInducing eâ‚) (hâ‚‚ : UniformInducing eâ‚‚) :
    UniformInducing fun p : Î± Ã— Î² => (eâ‚ p.1, eâ‚‚ p.2) :=
  âŸ¨by simp [(Â· âˆ˜ Â·), uniformity_prod, â† hâ‚.1, â† hâ‚‚.1, comap_inf, comap_comap]âŸ©
#align uniform_inducing.prod UniformInducing.prod

theorem UniformInducing.denseInducing {f : Î± â†’ Î²} (h : UniformInducing f) (hd : DenseRange f) :
    DenseInducing f :=
  { dense := hd
    induced := h.inducing.induced }
#align uniform_inducing.dense_inducing UniformInducing.denseInducing

theorem SeparationQuotient.uniformInducing_mk : UniformInducing (mk : Î± â†’ SeparationQuotient Î±) :=
  âŸ¨comap_mk_uniformityâŸ©

protected theorem UniformInducing.injective [T0Space Î±] {f : Î± â†’ Î²} (h : UniformInducing f) :
    Injective f :=
  h.inducing.injective

/-!
### Uniform embeddings
-/

/-- A map `f : Î± â†’ Î²` between uniform spaces is a *uniform embedding* if it is uniform inducing and
injective. If `Î±` is a separated space, then the latter assumption follows from the former. -/
@[mk_iff]
structure UniformEmbedding (f : Î± â†’ Î²) extends UniformInducing f : Prop where
  /-- A uniform embedding is injective. -/
  inj : Function.Injective f
#align uniform_embedding UniformEmbedding
#align uniform_embedding_iff uniformEmbedding_iff

theorem uniformEmbedding_iff' {f : Î± â†’ Î²} :
    UniformEmbedding f â†” Injective f âˆ§ UniformContinuous f âˆ§ comap (Prod.map f f) (ğ“¤ Î²) â‰¤ ğ“¤ Î± := by
  rw [uniformEmbedding_iff, and_comm, uniformInducing_iff']
#align uniform_embedding_iff' uniformEmbedding_iff'

theorem Filter.HasBasis.uniformEmbedding_iff' {Î¹ Î¹'} {p : Î¹ â†’ Prop} {p' : Î¹' â†’ Prop} {s s'}
    (h : (ğ“¤ Î±).HasBasis p s) (h' : (ğ“¤ Î²).HasBasis p' s') {f : Î± â†’ Î²} :
    UniformEmbedding f â†” Injective f âˆ§
      (âˆ€ i, p' i â†’ âˆƒ j, p j âˆ§ âˆ€ x y, (x, y) âˆˆ s j â†’ (f x, f y) âˆˆ s' i) âˆ§
        (âˆ€ j, p j â†’ âˆƒ i, p' i âˆ§ âˆ€ x y, (f x, f y) âˆˆ s' i â†’ (x, y) âˆˆ s j) := by
  rw [uniformEmbedding_iff, and_comm, h.uniformInducing_iff h']
#align filter.has_basis.uniform_embedding_iff' Filter.HasBasis.uniformEmbedding_iff'

theorem Filter.HasBasis.uniformEmbedding_iff {Î¹ Î¹'} {p : Î¹ â†’ Prop} {p' : Î¹' â†’ Prop} {s s'}
    (h : (ğ“¤ Î±).HasBasis p s) (h' : (ğ“¤ Î²).HasBasis p' s') {f : Î± â†’ Î²} :
    UniformEmbedding f â†” Injective f âˆ§ UniformContinuous f âˆ§
      (âˆ€ j, p j â†’ âˆƒ i, p' i âˆ§ âˆ€ x y, (f x, f y) âˆˆ s' i â†’ (x, y) âˆˆ s j) := by
  simp only [h.uniformEmbedding_iff' h', h.uniformContinuous_iff h']
#align filter.has_basis.uniform_embedding_iff Filter.HasBasis.uniformEmbedding_iff

theorem uniformEmbedding_subtype_val {p : Î± â†’ Prop} :
    UniformEmbedding (Subtype.val : Subtype p â†’ Î±) :=
  { comap_uniformity := rfl
    inj := Subtype.val_injective }
#align uniform_embedding_subtype_val uniformEmbedding_subtype_val
#align uniform_embedding_subtype_coe uniformEmbedding_subtype_val

theorem uniformEmbedding_set_inclusion {s t : Set Î±} (hst : s âŠ† t) :
    UniformEmbedding (inclusion hst) where
  comap_uniformity := by rw [uniformity_subtype, uniformity_subtype, comap_comap]; rfl
  inj := inclusion_injective hst
#align uniform_embedding_set_inclusion uniformEmbedding_set_inclusion

theorem UniformEmbedding.comp {g : Î² â†’ Î³} (hg : UniformEmbedding g) {f : Î± â†’ Î²}
    (hf : UniformEmbedding f) : UniformEmbedding (g âˆ˜ f) :=
  { hg.toUniformInducing.comp hf.toUniformInducing with inj := hg.inj.comp hf.inj }
#align uniform_embedding.comp UniformEmbedding.comp

theorem UniformEmbedding.of_comp_iff {g : Î² â†’ Î³} (hg : UniformEmbedding g) {f : Î± â†’ Î²} :
    UniformEmbedding (g âˆ˜ f) â†” UniformEmbedding f := by
  simp_rw [uniformEmbedding_iff, hg.toUniformInducing.of_comp_iff, hg.inj.of_comp_iff f]

theorem Equiv.uniformEmbedding {Î± Î² : Type*} [UniformSpace Î±] [UniformSpace Î²] (f : Î± â‰ƒ Î²)
    (hâ‚ : UniformContinuous f) (hâ‚‚ : UniformContinuous f.symm) : UniformEmbedding f :=
  uniformEmbedding_iff'.2 âŸ¨f.injective, hâ‚, by rwa [â† Equiv.prodCongr_apply, â† map_equiv_symm]âŸ©
#align equiv.uniform_embedding Equiv.uniformEmbedding

theorem uniformEmbedding_inl : UniformEmbedding (Sum.inl : Î± â†’ Î± âŠ• Î²) :=
  uniformEmbedding_iff'.2 âŸ¨Sum.inl_injective, uniformContinuous_inl, fun s hs =>
    âŸ¨Prod.map Sum.inl Sum.inl '' s âˆª range (Prod.map Sum.inr Sum.inr),
      union_mem_sup (image_mem_map hs) range_mem_map, fun x h => by simpa using hâŸ©âŸ©
#align uniform_embedding_inl uniformEmbedding_inl

theorem uniformEmbedding_inr : UniformEmbedding (Sum.inr : Î² â†’ Î± âŠ• Î²) :=
  uniformEmbedding_iff'.2 âŸ¨Sum.inr_injective, uniformContinuous_inr, fun s hs =>
    âŸ¨range (Prod.map Sum.inl Sum.inl) âˆª Prod.map Sum.inr Sum.inr '' s,
      union_mem_sup range_mem_map (image_mem_map hs), fun x h => by simpa using hâŸ©âŸ©
#align uniform_embedding_inr uniformEmbedding_inr

/-- If the domain of a `UniformInducing` map `f` is a Tâ‚€ space, then `f` is injective,
hence it is a `UniformEmbedding`. -/
protected theorem UniformInducing.uniformEmbedding [T0Space Î±] {f : Î± â†’ Î²}
    (hf : UniformInducing f) : UniformEmbedding f :=
  âŸ¨hf, hf.inducing.injectiveâŸ©
#align uniform_inducing.uniform_embedding UniformInducing.uniformEmbedding

theorem uniformEmbedding_iff_uniformInducing [T0Space Î±] {f : Î± â†’ Î²} :
    UniformEmbedding f â†” UniformInducing f :=
  âŸ¨UniformEmbedding.toUniformInducing, UniformInducing.uniformEmbeddingâŸ©
#align uniform_embedding_iff_uniform_inducing uniformEmbedding_iff_uniformInducing

/-- If a map `f : Î± â†’ Î²` sends any two distinct points to point that are **not** related by a fixed
`s âˆˆ ğ“¤ Î²`, then `f` is uniform inducing with respect to the discrete uniformity on `Î±`:
the preimage of `ğ“¤ Î²` under `Prod.map f f` is the principal filter generated by the diagonal in
`Î± Ã— Î±`. -/
theorem comap_uniformity_of_spaced_out {Î±} {f : Î± â†’ Î²} {s : Set (Î² Ã— Î²)} (hs : s âˆˆ ğ“¤ Î²)
    (hf : Pairwise fun x y => (f x, f y) âˆ‰ s) : comap (Prod.map f f) (ğ“¤ Î²) = ğ“Ÿ idRel := by
  refine le_antisymm ?_ (@refl_le_uniformity Î± (UniformSpace.comap f _))
  calc
    comap (Prod.map f f) (ğ“¤ Î²) â‰¤ comap (Prod.map f f) (ğ“Ÿ s) := comap_mono (le_principal_iff.2 hs)
    _ = ğ“Ÿ (Prod.map f f â»Â¹' s) := comap_principal
    _ â‰¤ ğ“Ÿ idRel := principal_mono.2 ?_
  rintro âŸ¨x, yâŸ©; simpa [not_imp_not] using @hf x y
#align comap_uniformity_of_spaced_out comap_uniformity_of_spaced_out

/-- If a map `f : Î± â†’ Î²` sends any two distinct points to point that are **not** related by a fixed
`s âˆˆ ğ“¤ Î²`, then `f` is a uniform embedding with respect to the discrete uniformity on `Î±`. -/
theorem uniformEmbedding_of_spaced_out {Î±} {f : Î± â†’ Î²} {s : Set (Î² Ã— Î²)} (hs : s âˆˆ ğ“¤ Î²)
    (hf : Pairwise fun x y => (f x, f y) âˆ‰ s) : @UniformEmbedding Î± Î² âŠ¥ â€¹_â€º f := by
  let _ : UniformSpace Î± := âŠ¥; have := discreteTopology_bot Î±
  exact UniformInducing.uniformEmbedding âŸ¨comap_uniformity_of_spaced_out hs hfâŸ©
#align uniform_embedding_of_spaced_out uniformEmbedding_of_spaced_out

protected theorem UniformEmbedding.embedding {f : Î± â†’ Î²} (h : UniformEmbedding f) : Embedding f :=
  { toInducing := h.toUniformInducing.inducing
    inj := h.inj }
#align uniform_embedding.embedding UniformEmbedding.embedding

theorem UniformEmbedding.denseEmbedding {f : Î± â†’ Î²} (h : UniformEmbedding f) (hd : DenseRange f) :
    DenseEmbedding f :=
  { h.embedding with dense := hd }
#align uniform_embedding.dense_embedding UniformEmbedding.denseEmbedding

theorem closedEmbedding_of_spaced_out {Î±} [TopologicalSpace Î±] [DiscreteTopology Î±]
    [T0Space Î²] {f : Î± â†’ Î²} {s : Set (Î² Ã— Î²)} (hs : s âˆˆ ğ“¤ Î²)
    (hf : Pairwise fun x y => (f x, f y) âˆ‰ s) : ClosedEmbedding f := by
  rcases @DiscreteTopology.eq_bot Î± _ _ with rfl; let _ : UniformSpace Î± := âŠ¥
  exact
    { (uniformEmbedding_of_spaced_out hs hf).embedding with
      isClosed_range := isClosed_range_of_spaced_out hs hf }
#align closed_embedding_of_spaced_out closedEmbedding_of_spaced_out

theorem closure_image_mem_nhds_of_uniformInducing {s : Set (Î± Ã— Î±)} {e : Î± â†’ Î²} (b : Î²)
    (heâ‚ : UniformInducing e) (heâ‚‚ : DenseInducing e) (hs : s âˆˆ ğ“¤ Î±) :
    âˆƒ a, closure (e '' { a' | (a, a') âˆˆ s }) âˆˆ ğ“ b := by
  obtain âŸ¨U, âŸ¨hU, hUo, hsymmâŸ©, hsâŸ© :
    âˆƒ U, (U âˆˆ ğ“¤ Î² âˆ§ IsOpen U âˆ§ SymmetricRel U) âˆ§ Prod.map e e â»Â¹' U âŠ† s := by
      rwa [â† heâ‚.comap_uniformity, (uniformity_hasBasis_open_symmetric.comap _).mem_iff] at hs
  rcases heâ‚‚.dense.mem_nhds (UniformSpace.ball_mem_nhds b hU) with âŸ¨a, haâŸ©
  refine âŸ¨a, mem_of_superset ?_ (closure_mono <| image_subset _ <| ball_mono hs a)âŸ©
  have ho : IsOpen (UniformSpace.ball (e a) U) := UniformSpace.isOpen_ball (e a) hUo
  refine mem_of_superset (ho.mem_nhds <| (mem_ball_symmetry hsymm).2 ha) fun y hy => ?_
  refine mem_closure_iff_nhds.2 fun V hV => ?_
  rcases heâ‚‚.dense.mem_nhds (inter_mem hV (ho.mem_nhds hy)) with âŸ¨x, hxV, hxUâŸ©
  exact âŸ¨e x, hxV, mem_image_of_mem e hxUâŸ©
#align closure_image_mem_nhds_of_uniform_inducing closure_image_mem_nhds_of_uniformInducing

theorem uniformEmbedding_subtypeEmb (p : Î± â†’ Prop) {e : Î± â†’ Î²} (ue : UniformEmbedding e)
    (de : DenseEmbedding e) : UniformEmbedding (DenseEmbedding.subtypeEmb p e) :=
  { comap_uniformity := by
      simp [comap_comap, (Â· âˆ˜ Â·), DenseEmbedding.subtypeEmb, uniformity_subtype,
        ue.comap_uniformity.symm]
    inj := (de.subtype p).inj }
#align uniform_embedding_subtype_emb uniformEmbedding_subtypeEmb

theorem UniformEmbedding.prod {Î±' : Type*} {Î²' : Type*} [UniformSpace Î±'] [UniformSpace Î²']
    {eâ‚ : Î± â†’ Î±'} {eâ‚‚ : Î² â†’ Î²'} (hâ‚ : UniformEmbedding eâ‚) (hâ‚‚ : UniformEmbedding eâ‚‚) :
    UniformEmbedding fun p : Î± Ã— Î² => (eâ‚ p.1, eâ‚‚ p.2) :=
  { hâ‚.toUniformInducing.prod hâ‚‚.toUniformInducing with inj := hâ‚.inj.prodMap hâ‚‚.inj }
#align uniform_embedding.prod UniformEmbedding.prod

/-- A set is complete iff its image under a uniform inducing map is complete. -/
theorem isComplete_image_iff {m : Î± â†’ Î²} {s : Set Î±} (hm : UniformInducing m) :
    IsComplete (m '' s) â†” IsComplete s := by
  have fact1 : SurjOn (map m) (Iic <| ğ“Ÿ s) (Iic <| ğ“Ÿ <| m '' s) := surjOn_image .. |>.filter_map_Iic
  have fact2 : MapsTo (map m) (Iic <| ğ“Ÿ s) (Iic <| ğ“Ÿ <| m '' s) := mapsTo_image .. |>.filter_map_Iic
  simp_rw [IsComplete, imp.swap (a := Cauchy _), â† mem_Iic (b := ğ“Ÿ _), fact1.forall fact2,
    hm.cauchy_map_iff, exists_mem_image, map_le_iff_le_comap, hm.inducing.nhds_eq_comap]
#align is_complete_image_iff isComplete_image_iff

alias âŸ¨isComplete_of_complete_image, _âŸ© := isComplete_image_iff
#align is_complete_of_complete_image isComplete_of_complete_image

theorem completeSpace_iff_isComplete_range {f : Î± â†’ Î²} (hf : UniformInducing f) :
    CompleteSpace Î± â†” IsComplete (range f) := by
  rw [completeSpace_iff_isComplete_univ, â† isComplete_image_iff hf, image_univ]
#align complete_space_iff_is_complete_range completeSpace_iff_isComplete_range

theorem UniformInducing.isComplete_range [CompleteSpace Î±] {f : Î± â†’ Î²} (hf : UniformInducing f) :
    IsComplete (range f) :=
  (completeSpace_iff_isComplete_range hf).1 â€¹_â€º
#align uniform_inducing.is_complete_range UniformInducing.isComplete_range

theorem SeparationQuotient.completeSpace_iff :
    CompleteSpace (SeparationQuotient Î±) â†” CompleteSpace Î± := by
  rw [completeSpace_iff_isComplete_univ, â† range_mk,
    â† completeSpace_iff_isComplete_range uniformInducing_mk]

instance SeparationQuotient.instCompleteSpace [CompleteSpace Î±] :
    CompleteSpace (SeparationQuotient Î±) :=
  completeSpace_iff.2 â€¹_â€º
#align uniform_space.complete_space_separation SeparationQuotient.instCompleteSpace

theorem completeSpace_congr {e : Î± â‰ƒ Î²} (he : UniformEmbedding e) :
    CompleteSpace Î± â†” CompleteSpace Î² := by
  rw [completeSpace_iff_isComplete_range he.toUniformInducing, e.range_eq_univ,
    completeSpace_iff_isComplete_univ]
#align complete_space_congr completeSpace_congr

theorem completeSpace_coe_iff_isComplete {s : Set Î±} : CompleteSpace s â†” IsComplete s :=
  (completeSpace_iff_isComplete_range uniformEmbedding_subtype_val.toUniformInducing).trans <| by
    rw [Subtype.range_coe]
#align complete_space_coe_iff_is_complete completeSpace_coe_iff_isComplete

alias âŸ¨_, IsComplete.completeSpace_coeâŸ© := completeSpace_coe_iff_isComplete
#align is_complete.complete_space_coe IsComplete.completeSpace_coe

theorem IsClosed.completeSpace_coe [CompleteSpace Î±] {s : Set Î±} (hs : IsClosed s) :
    CompleteSpace s :=
  hs.isComplete.completeSpace_coe
#align is_closed.complete_space_coe IsClosed.completeSpace_coe

/-- The lift of a complete space to another universe is still complete. -/
instance ULift.completeSpace [h : CompleteSpace Î±] : CompleteSpace (ULift Î±) :=
  haveI : UniformEmbedding (@Equiv.ulift Î±) := âŸ¨âŸ¨rflâŸ©, ULift.down_injectiveâŸ©
  (completeSpace_congr this).2 h
#align ulift.complete_space ULift.completeSpace

theorem completeSpace_extension {m : Î² â†’ Î±} (hm : UniformInducing m) (dense : DenseRange m)
    (h : âˆ€ f : Filter Î², Cauchy f â†’ âˆƒ x : Î±, map m f â‰¤ ğ“ x) : CompleteSpace Î± :=
  âŸ¨fun {f : Filter Î±} (hf : Cauchy f) =>
    let p : Set (Î± Ã— Î±) â†’ Set Î± â†’ Set Î± := fun s t => { y : Î± | âˆƒ x : Î±, x âˆˆ t âˆ§ (x, y) âˆˆ s }
    let g := (ğ“¤ Î±).lift fun s => f.lift' (p s)
    have mpâ‚€ : Monotone p := fun a b h t s âŸ¨x, xs, xaâŸ© => âŸ¨x, xs, h xaâŸ©
    have mpâ‚ : âˆ€ {s}, Monotone (p s) := fun h x âŸ¨y, ya, yxsâŸ© => âŸ¨y, h ya, yxsâŸ©
    have : f â‰¤ g := le_iInfâ‚‚ fun s hs => le_iInfâ‚‚ fun t ht =>
      le_principal_iff.mpr <| mem_of_superset ht fun x hx => âŸ¨x, hx, refl_mem_uniformity hsâŸ©
    have : NeBot g := hf.left.mono this
    have : NeBot (comap m g) :=
      comap_neBot fun t ht =>
        let âŸ¨t', ht', ht_memâŸ© := (mem_lift_sets <| monotone_lift' monotone_const mpâ‚€).mp ht
        let âŸ¨t'', ht'', ht'_subâŸ© := (mem_lift'_sets mpâ‚).mp ht_mem
        let âŸ¨x, (hx : x âˆˆ t'')âŸ© := hf.left.nonempty_of_mem ht''
        have hâ‚€ : NeBot (ğ“[range m] x) := dense.nhdsWithin_neBot x
        have hâ‚ : { y | (x, y) âˆˆ t' } âˆˆ ğ“[range m] x :=
          @mem_inf_of_left Î± (ğ“ x) (ğ“Ÿ (range m)) _ <| mem_nhds_left x ht'
        have hâ‚‚ : range m âˆˆ ğ“[range m] x :=
          @mem_inf_of_right Î± (ğ“ x) (ğ“Ÿ (range m)) _ <| Subset.refl _
        have : { y | (x, y) âˆˆ t' } âˆ© range m âˆˆ ğ“[range m] x := @inter_mem Î± (ğ“[range m] x) _ _ hâ‚ hâ‚‚
        let âŸ¨y, xyt', b, b_eqâŸ© := hâ‚€.nonempty_of_mem this
        âŸ¨b, b_eq.symm â–¸ ht'_sub âŸ¨x, hx, xyt'âŸ©âŸ©
    have : Cauchy g :=
      âŸ¨â€¹NeBot gâ€º, fun s hs =>
        let âŸ¨sâ‚, hsâ‚, (comp_sâ‚ : compRel sâ‚ sâ‚ âŠ† s)âŸ© := comp_mem_uniformity_sets hs
        let âŸ¨sâ‚‚, hsâ‚‚, (comp_sâ‚‚ : compRel sâ‚‚ sâ‚‚ âŠ† sâ‚)âŸ© := comp_mem_uniformity_sets hsâ‚
        let âŸ¨t, ht, (prod_t : t Ã—Ë¢ t âŠ† sâ‚‚)âŸ© := mem_prod_same_iff.mp (hf.right hsâ‚‚)
        have hgâ‚ : p (preimage Prod.swap sâ‚) t âˆˆ g :=
          mem_lift (symm_le_uniformity hsâ‚) <| @mem_lift' Î± Î± f _ t ht
        have hgâ‚‚ : p sâ‚‚ t âˆˆ g := mem_lift hsâ‚‚ <| @mem_lift' Î± Î± f _ t ht
        have hg : p (Prod.swap â»Â¹' sâ‚) t Ã—Ë¢ p sâ‚‚ t âˆˆ g Ã—Ë¢ g := @prod_mem_prod Î± Î± _ _ g g hgâ‚ hgâ‚‚
        (g Ã—Ë¢ g).sets_of_superset hg fun âŸ¨a, bâŸ© âŸ¨âŸ¨câ‚, câ‚t, hcâ‚âŸ©, âŸ¨câ‚‚, câ‚‚t, hcâ‚‚âŸ©âŸ© =>
          have : (câ‚, câ‚‚) âˆˆ t Ã—Ë¢ t := âŸ¨câ‚t, câ‚‚tâŸ©
          comp_sâ‚ <| prod_mk_mem_compRel hcâ‚ <| comp_sâ‚‚ <| prod_mk_mem_compRel (prod_t this) hcâ‚‚âŸ©
    have : Cauchy (Filter.comap m g) := â€¹Cauchy gâ€º.comap' (le_of_eq hm.comap_uniformity) â€¹_â€º
    let âŸ¨x, (hx : map m (Filter.comap m g) â‰¤ ğ“ x)âŸ© := h _ this
    have : ClusterPt x (map m (Filter.comap m g)) :=
      (le_nhds_iff_adhp_of_cauchy (this.map hm.uniformContinuous)).mp hx
    have : ClusterPt x g := this.mono map_comap_le
    âŸ¨x,
      calc
        f â‰¤ g := by assumption
        _ â‰¤ ğ“ x := le_nhds_of_cauchy_adhp â€¹Cauchy gâ€º this
        âŸ©âŸ©
#align complete_space_extension completeSpace_extension

theorem totallyBounded_preimage {f : Î± â†’ Î²} {s : Set Î²} (hf : UniformEmbedding f)
    (hs : TotallyBounded s) : TotallyBounded (f â»Â¹' s) := fun t ht => by
  rw [â† hf.comap_uniformity] at ht
  rcases mem_comap.2 ht with âŸ¨t', ht', tsâŸ©
  rcases totallyBounded_iff_subset.1 (totallyBounded_subset (image_preimage_subset f s) hs) _ ht'
    with âŸ¨c, cs, hfc, hctâŸ©
  refine âŸ¨f â»Â¹' c, hfc.preimage hf.inj.injOn, fun x h => ?_âŸ©
  have := hct (mem_image_of_mem f h); simp at this âŠ¢
  rcases this with âŸ¨z, zc, ztâŸ©
  rcases cs zc with âŸ¨y, -, rflâŸ©
  exact âŸ¨y, zc, ts ztâŸ©
#align totally_bounded_preimage totallyBounded_preimage

instance CompleteSpace.sum [CompleteSpace Î±] [CompleteSpace Î²] : CompleteSpace (Sum Î± Î²) := by
  rw [completeSpace_iff_isComplete_univ, â† range_inl_union_range_inr]
  exact uniformEmbedding_inl.toUniformInducing.isComplete_range.union
    uniformEmbedding_inr.toUniformInducing.isComplete_range
#align complete_space.sum CompleteSpace.sum

end

theorem uniformEmbedding_comap {Î± : Type*} {Î² : Type*} {f : Î± â†’ Î²} [u : UniformSpace Î²]
    (hf : Function.Injective f) : @UniformEmbedding Î± Î² (UniformSpace.comap f u) u f :=
  @UniformEmbedding.mk _ _ (UniformSpace.comap f u) _ _
    (@UniformInducing.mk _ _ (UniformSpace.comap f u) _ _ rfl) hf
#align uniform_embedding_comap uniformEmbedding_comap

/-- Pull back a uniform space structure by an embedding, adjusting the new uniform structure to
make sure that its topology is defeq to the original one. -/
def Embedding.comapUniformSpace {Î± Î²} [TopologicalSpace Î±] [u : UniformSpace Î²] (f : Î± â†’ Î²)
    (h : Embedding f) : UniformSpace Î± :=
  (u.comap f).replaceTopology h.induced
#align embedding.comap_uniform_space Embedding.comapUniformSpace

theorem Embedding.to_uniformEmbedding {Î± Î²} [TopologicalSpace Î±] [u : UniformSpace Î²] (f : Î± â†’ Î²)
    (h : Embedding f) : @UniformEmbedding Î± Î² (h.comapUniformSpace f) u f :=
  let _ := h.comapUniformSpace f
  { comap_uniformity := rfl
    inj := h.inj }
#align embedding.to_uniform_embedding Embedding.to_uniformEmbedding

section UniformExtension

variable {Î± : Type*} {Î² : Type*} {Î³ : Type*} [UniformSpace Î±] [UniformSpace Î²] [UniformSpace Î³]
  {e : Î² â†’ Î±} (h_e : UniformInducing e) (h_dense : DenseRange e) {f : Î² â†’ Î³}
  (h_f : UniformContinuous f)

local notation "Ïˆ" => DenseInducing.extend (UniformInducing.denseInducing h_e h_dense) f

theorem uniformly_extend_exists [CompleteSpace Î³] (a : Î±) : âˆƒ c, Tendsto f (comap e (ğ“ a)) (ğ“ c) :=
  let de := h_e.denseInducing h_dense
  have : Cauchy (ğ“ a) := cauchy_nhds
  have : Cauchy (comap e (ğ“ a)) :=
    this.comap' (le_of_eq h_e.comap_uniformity) (de.comap_nhds_neBot _)
  have : Cauchy (map f (comap e (ğ“ a))) := this.map h_f
  CompleteSpace.complete this
#align uniformly_extend_exists uniformly_extend_exists

theorem uniform_extend_subtype [CompleteSpace Î³] {p : Î± â†’ Prop} {e : Î± â†’ Î²} {f : Î± â†’ Î³} {b : Î²}
    {s : Set Î±} (hf : UniformContinuous fun x : Subtype p => f x.val) (he : UniformEmbedding e)
    (hd : âˆ€ x : Î², x âˆˆ closure (range e)) (hb : closure (e '' s) âˆˆ ğ“ b) (hs : IsClosed s)
    (hp : âˆ€ x âˆˆ s, p x) : âˆƒ c, Tendsto f (comap e (ğ“ b)) (ğ“ c) := by
  have de : DenseEmbedding e := he.denseEmbedding hd
  have de' : DenseEmbedding (DenseEmbedding.subtypeEmb p e) := de.subtype p
  have ue' : UniformEmbedding (DenseEmbedding.subtypeEmb p e) := uniformEmbedding_subtypeEmb _ he de
  have : b âˆˆ closure (e '' { x | p x }) :=
    (closure_mono <| monotone_image <| hp) (mem_of_mem_nhds hb)
  let âŸ¨c, hcâŸ© := uniformly_extend_exists ue'.toUniformInducing de'.dense hf âŸ¨b, thisâŸ©
  replace hc : Tendsto (f âˆ˜ Subtype.val (p := p)) (((ğ“ b).comap e).comap Subtype.val) (ğ“ c) := by
    simpa only [nhds_subtype_eq_comap, comap_comap, DenseEmbedding.subtypeEmb_coe] using hc
  refine âŸ¨c, (tendsto_comap'_iff ?_).1 hcâŸ©
  rw [Subtype.range_coe_subtype]
  exact âŸ¨_, hb, by rwa [â† de.toInducing.closure_eq_preimage_closure_image, hs.closure_eq]âŸ©
#align uniform_extend_subtype uniform_extend_subtype

theorem uniformly_extend_spec [CompleteSpace Î³] (a : Î±) : Tendsto f (comap e (ğ“ a)) (ğ“ (Ïˆ a)) := by
  simpa only [DenseInducing.extend] using
    tendsto_nhds_limUnder (uniformly_extend_exists h_e â€¹_â€º h_f _)
#align uniformly_extend_spec uniformly_extend_spec

theorem uniformContinuous_uniformly_extend [CompleteSpace Î³] : UniformContinuous Ïˆ := fun d hd =>
  let âŸ¨s, hs, hs_compâŸ© := comp3_mem_uniformity hd
  have h_pnt : âˆ€ {a m}, m âˆˆ ğ“ a â†’ âˆƒ c âˆˆ f '' (e â»Â¹' m), (c, Ïˆ a) âˆˆ s âˆ§ (Ïˆ a, c) âˆˆ s :=
    fun {a m} hm =>
    have nb : NeBot (map f (comap e (ğ“ a))) :=
      ((h_e.denseInducing h_dense).comap_nhds_neBot _).map _
    have :
      f '' (e â»Â¹' m) âˆ© ({ c | (c, Ïˆ a) âˆˆ s } âˆ© { c | (Ïˆ a, c) âˆˆ s }) âˆˆ map f (comap e (ğ“ a)) :=
      inter_mem (image_mem_map <| preimage_mem_comap <| hm)
        (uniformly_extend_spec h_e h_dense h_f _
          (inter_mem (mem_nhds_right _ hs) (mem_nhds_left _ hs)))
    nb.nonempty_of_mem this
  have : (Prod.map f f) â»Â¹' s âˆˆ ğ“¤ Î² := h_f hs
  have : (Prod.map f f) â»Â¹' s âˆˆ comap (Prod.map e e) (ğ“¤ Î±) := by
    rwa [â† h_e.comap_uniformity] at this
  let âŸ¨t, ht, tsâŸ© := this
  show (Prod.map Ïˆ Ïˆ) â»Â¹' d âˆˆ ğ“¤ Î± from
    mem_of_superset (interior_mem_uniformity ht) fun âŸ¨xâ‚, xâ‚‚âŸ© hx_t => by
      have : interior t âˆˆ ğ“ (xâ‚, xâ‚‚) := isOpen_interior.mem_nhds hx_t
      let âŸ¨mâ‚, hmâ‚, mâ‚‚, hmâ‚‚, (hm : mâ‚ Ã—Ë¢ mâ‚‚ âŠ† interior t)âŸ© := mem_nhds_prod_iff.mp this
      obtain âŸ¨_, âŸ¨a, haâ‚, rflâŸ©, _, haâ‚‚âŸ© := h_pnt hmâ‚
      obtain âŸ¨_, âŸ¨b, hbâ‚, rflâŸ©, hbâ‚‚, _âŸ© := h_pnt hmâ‚‚
      have : Prod.map f f (a, b) âˆˆ s :=
        ts <| mem_preimage.2 <| interior_subset (@hm (e a, e b) âŸ¨haâ‚, hbâ‚âŸ©)
      exact hs_comp âŸ¨f a, haâ‚‚, âŸ¨f b, this, hbâ‚‚âŸ©âŸ©
#align uniform_continuous_uniformly_extend uniformContinuous_uniformly_extend

variable [T0Space Î³]

theorem uniformly_extend_of_ind (b : Î²) : Ïˆ (e b) = f b :=
  DenseInducing.extend_eq_at _ h_f.continuous.continuousAt
#align uniformly_extend_of_ind uniformly_extend_of_ind

theorem uniformly_extend_unique {g : Î± â†’ Î³} (hg : âˆ€ b, g (e b) = f b) (hc : Continuous g) : Ïˆ = g :=
  DenseInducing.extend_unique _ hg hc
#align uniformly_extend_unique uniformly_extend_unique

end UniformExtension
