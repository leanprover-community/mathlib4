/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, SÃ©bastien GouÃ«zel, Patrick Massot
-/
import Mathlib.Topology.UniformSpace.Cauchy
import Mathlib.Topology.UniformSpace.Separation
import Mathlib.Topology.DenseEmbedding

/-!
# Uniform embeddings of uniform spaces.

Extension of uniform continuous functions.
-/


open Filter Function Set Uniformity Topology
open scoped SetRel

section

universe u v w
variable {Î± : Type u} {Î² : Type v} {Î³ : Type w} [UniformSpace Î±] [UniformSpace Î²] [UniformSpace Î³]
  {f : Î± â†’ Î²}

/-!
### Uniform inducing maps
-/

/-- A map `f : Î± â†’ Î²` between uniform spaces is called *uniform inducing* if the uniformity filter
on `Î±` is the pullback of the uniformity filter on `Î²` under `Prod.map f f`. If `Î±` is a separated
space, then this implies that `f` is injective, hence it is a `IsUniformEmbedding`. -/
@[mk_iff]
structure IsUniformInducing (f : Î± â†’ Î²) : Prop where
  /-- The uniformity filter on the domain is the pullback of the uniformity filter on the codomain
  under `Prod.map f f`. -/
  comap_uniformity : comap (fun x : Î± Ã— Î± => (f x.1, f x.2)) (ğ“¤ Î²) = ğ“¤ Î±

lemma isUniformInducing_iff_uniformSpace {f : Î± â†’ Î²} :
    IsUniformInducing f â†” â€¹UniformSpace Î²â€º.comap f = â€¹UniformSpace Î±â€º := by
  rw [isUniformInducing_iff, UniformSpace.ext_iff, Filter.ext_iff]
  rfl

protected alias âŸ¨IsUniformInducing.comap_uniformSpace, _âŸ© := isUniformInducing_iff_uniformSpace

lemma isUniformInducing_iff' {f : Î± â†’ Î²} :
    IsUniformInducing f â†” UniformContinuous f âˆ§ comap (Prod.map f f) (ğ“¤ Î²) â‰¤ ğ“¤ Î± := by
  rw [isUniformInducing_iff, UniformContinuous, tendsto_iff_comap, le_antisymm_iff, and_comm]; rfl

protected lemma Filter.HasBasis.isUniformInducing_iff {Î¹ Î¹'} {p : Î¹ â†’ Prop} {p' : Î¹' â†’ Prop} {s s'}
    (h : (ğ“¤ Î±).HasBasis p s) (h' : (ğ“¤ Î²).HasBasis p' s') {f : Î± â†’ Î²} :
    IsUniformInducing f â†”
      (âˆ€ i, p' i â†’ âˆƒ j, p j âˆ§ âˆ€ x y, (x, y) âˆˆ s j â†’ (f x, f y) âˆˆ s' i) âˆ§
        (âˆ€ j, p j â†’ âˆƒ i, p' i âˆ§ âˆ€ x y, (f x, f y) âˆˆ s' i â†’ (x, y) âˆˆ s j) := by
  simp [isUniformInducing_iff', h.uniformContinuous_iff h', (h'.comap _).le_basis_iff h, subset_def]

theorem IsUniformInducing.mk' {f : Î± â†’ Î²}
    (h : âˆ€ s, s âˆˆ ğ“¤ Î± â†” âˆƒ t âˆˆ ğ“¤ Î², âˆ€ x y : Î±, (f x, f y) âˆˆ t â†’ (x, y) âˆˆ s) : IsUniformInducing f :=
  âŸ¨by simp [eq_comm, Filter.ext_iff, subset_def, h]âŸ©

theorem IsUniformInducing.id : IsUniformInducing (@id Î±) :=
  âŸ¨by rw [â† Prod.map_def, Prod.map_id, comap_id]âŸ©

theorem IsUniformInducing.comp {g : Î² â†’ Î³} (hg : IsUniformInducing g) {f : Î± â†’ Î²}
    (hf : IsUniformInducing f) : IsUniformInducing (g âˆ˜ f) :=
  âŸ¨by rw [â† hf.1, â† hg.1, comap_comap]; rflâŸ©

theorem IsUniformInducing.of_comp_iff {g : Î² â†’ Î³} (hg : IsUniformInducing g) {f : Î± â†’ Î²} :
    IsUniformInducing (g âˆ˜ f) â†” IsUniformInducing f := by
  refine âŸ¨fun h â†¦ ?_, hg.compâŸ©
  rw [isUniformInducing_iff, â† hg.comap_uniformity, comap_comap, â† h.comap_uniformity,
    Function.comp_def, Function.comp_def]

theorem IsUniformInducing.basis_uniformity {f : Î± â†’ Î²} (hf : IsUniformInducing f) {Î¹ : Sort*}
    {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set (Î² Ã— Î²)} (H : (ğ“¤ Î²).HasBasis p s) :
    (ğ“¤ Î±).HasBasis p fun i => Prod.map f f â»Â¹' s i :=
  hf.1 â–¸ H.comap _

theorem IsUniformInducing.cauchy_map_iff {f : Î± â†’ Î²} (hf : IsUniformInducing f) {F : Filter Î±} :
    Cauchy (map f F) â†” Cauchy F := by
  simp only [Cauchy, map_neBot_iff, prod_map_map_eq, map_le_iff_le_comap, â† hf.comap_uniformity]

theorem IsUniformInducing.of_comp {f : Î± â†’ Î²} {g : Î² â†’ Î³} (hf : UniformContinuous f)
    (hg : UniformContinuous g) (hgf : IsUniformInducing (g âˆ˜ f)) : IsUniformInducing f := by
  refine âŸ¨le_antisymm ?_ hf.le_comapâŸ©
  rw [â† hgf.1, â† Prod.map_def, â† Prod.map_def, â† Prod.map_comp_map f f g g, â† comap_comap]
  exact comap_mono hg.le_comap

theorem IsUniformInducing.uniformContinuous {f : Î± â†’ Î²} (hf : IsUniformInducing f) :
    UniformContinuous f := (isUniformInducing_iff'.1 hf).1

theorem IsUniformInducing.uniformContinuous_iff {f : Î± â†’ Î²} {g : Î² â†’ Î³} (hg : IsUniformInducing g) :
    UniformContinuous f â†” UniformContinuous (g âˆ˜ f) := by
  dsimp only [UniformContinuous, Tendsto]
  simp only [â† hg.comap_uniformity, â† map_le_iff_le_comap, Filter.map_map, Function.comp_def]

protected theorem IsUniformInducing.isUniformInducing_comp_iff {f : Î± â†’ Î²} {g : Î² â†’ Î³}
    (hg : IsUniformInducing g) : IsUniformInducing (g âˆ˜ f) â†” IsUniformInducing f := by
  simp only [isUniformInducing_iff, â† hg.comap_uniformity, comap_comap, Function.comp_def]

theorem IsUniformInducing.uniformContinuousOn_iff {f : Î± â†’ Î²} {g : Î² â†’ Î³} {S : Set Î±}
    (hg : IsUniformInducing g) :
    UniformContinuousOn f S â†” UniformContinuousOn (g âˆ˜ f) S := by
  dsimp only [UniformContinuousOn, Tendsto]
  rw [â† hg.comap_uniformity, â† map_le_iff_le_comap, Filter.map_map, comp_def, comp_def]

theorem IsUniformInducing.isInducing {f : Î± â†’ Î²} (h : IsUniformInducing f) : IsInducing f := by
  obtain rfl := h.comap_uniformSpace
  exact .induced f

theorem IsUniformInducing.prod {Î±' : Type*} {Î²' : Type*} [UniformSpace Î±'] [UniformSpace Î²']
    {eâ‚ : Î± â†’ Î±'} {eâ‚‚ : Î² â†’ Î²'} (hâ‚ : IsUniformInducing eâ‚) (hâ‚‚ : IsUniformInducing eâ‚‚) :
    IsUniformInducing fun p : Î± Ã— Î² => (eâ‚ p.1, eâ‚‚ p.2) :=
  âŸ¨by simp [Function.comp_def, uniformity_prod, â† hâ‚.1, â† hâ‚‚.1, comap_inf, comap_comap]âŸ©

lemma IsUniformInducing.isDenseInducing (h : IsUniformInducing f) (hd : DenseRange f) :
    IsDenseInducing f where
  toIsInducing := h.isInducing
  dense := hd

lemma SeparationQuotient.isUniformInducing_mk :
    IsUniformInducing (mk : Î± â†’ SeparationQuotient Î±) :=
  âŸ¨comap_mk_uniformityâŸ©

protected theorem IsUniformInducing.injective [T0Space Î±] {f : Î± â†’ Î²} (h : IsUniformInducing f) :
    Injective f :=
  h.isInducing.injective

/-!
### Uniform embeddings
-/

/-- A map `f : Î± â†’ Î²` between uniform spaces is a *uniform embedding* if it is uniform inducing and
injective. If `Î±` is a separated space, then the latter assumption follows from the former. -/
@[mk_iff]
structure IsUniformEmbedding (f : Î± â†’ Î²) : Prop extends IsUniformInducing f where
  /-- A uniform embedding is injective. -/
  injective : Function.Injective f

lemma IsUniformEmbedding.isUniformInducing (hf : IsUniformEmbedding f) : IsUniformInducing f :=
  hf.toIsUniformInducing

theorem isUniformEmbedding_iff' {f : Î± â†’ Î²} :
    IsUniformEmbedding f â†”
      Injective f âˆ§ UniformContinuous f âˆ§ comap (Prod.map f f) (ğ“¤ Î²) â‰¤ ğ“¤ Î± := by
  rw [isUniformEmbedding_iff, and_comm, isUniformInducing_iff']

theorem Filter.HasBasis.isUniformEmbedding_iff' {Î¹ Î¹'} {p : Î¹ â†’ Prop} {p' : Î¹' â†’ Prop} {s s'}
    (h : (ğ“¤ Î±).HasBasis p s) (h' : (ğ“¤ Î²).HasBasis p' s') {f : Î± â†’ Î²} :
    IsUniformEmbedding f â†” Injective f âˆ§
      (âˆ€ i, p' i â†’ âˆƒ j, p j âˆ§ âˆ€ x y, (x, y) âˆˆ s j â†’ (f x, f y) âˆˆ s' i) âˆ§
        (âˆ€ j, p j â†’ âˆƒ i, p' i âˆ§ âˆ€ x y, (f x, f y) âˆˆ s' i â†’ (x, y) âˆˆ s j) := by
  rw [isUniformEmbedding_iff, and_comm, h.isUniformInducing_iff h']

theorem Filter.HasBasis.isUniformEmbedding_iff {Î¹ Î¹'} {p : Î¹ â†’ Prop} {p' : Î¹' â†’ Prop} {s s'}
    (h : (ğ“¤ Î±).HasBasis p s) (h' : (ğ“¤ Î²).HasBasis p' s') {f : Î± â†’ Î²} :
    IsUniformEmbedding f â†” Injective f âˆ§ UniformContinuous f âˆ§
      (âˆ€ j, p j â†’ âˆƒ i, p' i âˆ§ âˆ€ x y, (f x, f y) âˆˆ s' i â†’ (x, y) âˆˆ s j) := by
  simp only [h.isUniformEmbedding_iff' h', h.uniformContinuous_iff h']

theorem isUniformEmbedding_subtype_val {p : Î± â†’ Prop} :
    IsUniformEmbedding (Subtype.val : Subtype p â†’ Î±) :=
  { comap_uniformity := rfl
    injective := Subtype.val_injective }

theorem isUniformEmbedding_set_inclusion {s t : Set Î±} (hst : s âŠ† t) :
    IsUniformEmbedding (inclusion hst) where
  comap_uniformity := by rw [uniformity_subtype, uniformity_subtype, comap_comap]; rfl
  injective := inclusion_injective hst

theorem IsUniformEmbedding.comp {g : Î² â†’ Î³} (hg : IsUniformEmbedding g) {f : Î± â†’ Î²}
    (hf : IsUniformEmbedding f) : IsUniformEmbedding (g âˆ˜ f) where
  toIsUniformInducing := hg.isUniformInducing.comp hf.isUniformInducing
  injective := hg.injective.comp hf.injective

theorem IsUniformEmbedding.of_comp_iff {g : Î² â†’ Î³} (hg : IsUniformEmbedding g) {f : Î± â†’ Î²} :
    IsUniformEmbedding (g âˆ˜ f) â†” IsUniformEmbedding f := by
  simp_rw [isUniformEmbedding_iff, hg.isUniformInducing.of_comp_iff, hg.injective.of_comp_iff f]

theorem Equiv.isUniformEmbedding {Î± Î² : Type*} [UniformSpace Î±] [UniformSpace Î²] (f : Î± â‰ƒ Î²)
    (hâ‚ : UniformContinuous f) (hâ‚‚ : UniformContinuous f.symm) : IsUniformEmbedding f :=
  isUniformEmbedding_iff'.2 âŸ¨f.injective, hâ‚, by rwa [â† Equiv.prodCongr_apply, â† map_equiv_symm]âŸ©

theorem isUniformEmbedding_inl : IsUniformEmbedding (Sum.inl : Î± â†’ Î± âŠ• Î²) :=
  isUniformEmbedding_iff'.2 âŸ¨Sum.inl_injective, uniformContinuous_inl, fun s hs =>
    âŸ¨Prod.map Sum.inl Sum.inl '' s âˆª range (Prod.map Sum.inr Sum.inr),
      union_mem_sup (image_mem_map hs) range_mem_map,
      fun x h => by simpa [Prod.map_apply'] using hâŸ©âŸ©

theorem isUniformEmbedding_inr : IsUniformEmbedding (Sum.inr : Î² â†’ Î± âŠ• Î²) :=
  isUniformEmbedding_iff'.2 âŸ¨Sum.inr_injective, uniformContinuous_inr, fun s hs =>
    âŸ¨range (Prod.map Sum.inl Sum.inl) âˆª Prod.map Sum.inr Sum.inr '' s,
      union_mem_sup range_mem_map (image_mem_map hs),
      fun x h => by simpa [Prod.map_apply'] using hâŸ©âŸ©

/-- If the domain of a `IsUniformInducing` map `f` is a Tâ‚€ space, then `f` is injective,
hence it is a `IsUniformEmbedding`. -/
protected theorem IsUniformInducing.isUniformEmbedding [T0Space Î±] {f : Î± â†’ Î²}
    (hf : IsUniformInducing f) : IsUniformEmbedding f :=
  âŸ¨hf, hf.isInducing.injectiveâŸ©

theorem isUniformEmbedding_iff_isUniformInducing [T0Space Î±] {f : Î± â†’ Î²} :
    IsUniformEmbedding f â†” IsUniformInducing f :=
  âŸ¨IsUniformEmbedding.isUniformInducing, IsUniformInducing.isUniformEmbeddingâŸ©

/-- If a map `f : Î± â†’ Î²` sends any two distinct points to point that are **not** related by a fixed
`s âˆˆ ğ“¤ Î²`, then `f` is uniform inducing with respect to the discrete uniformity on `Î±`:
the preimage of `ğ“¤ Î²` under `Prod.map f f` is the principal filter generated by the diagonal in
`Î± Ã— Î±`. -/
theorem comap_uniformity_of_spaced_out {Î±} {f : Î± â†’ Î²} {s : Set (Î² Ã— Î²)} (hs : s âˆˆ ğ“¤ Î²)
    (hf : Pairwise fun x y => (f x, f y) âˆ‰ s) : comap (Prod.map f f) (ğ“¤ Î²) = ğ“Ÿ SetRel.id := by
  refine le_antisymm ?_ (@refl_le_uniformity Î± (UniformSpace.comap f _))
  calc
    comap (Prod.map f f) (ğ“¤ Î²) â‰¤ comap (Prod.map f f) (ğ“Ÿ s) := comap_mono (le_principal_iff.2 hs)
    _ = ğ“Ÿ (Prod.map f f â»Â¹' s) := comap_principal
    _ â‰¤ ğ“Ÿ SetRel.id := principal_mono.2 ?_
  rintro âŸ¨x, yâŸ©; simpa [not_imp_not] using @hf x y

/-- If a map `f : Î± â†’ Î²` sends any two distinct points to point that are **not** related by a fixed
`s âˆˆ ğ“¤ Î²`, then `f` is a uniform embedding with respect to the discrete uniformity on `Î±`. -/
theorem isUniformEmbedding_of_spaced_out {Î±} {f : Î± â†’ Î²} {s : Set (Î² Ã— Î²)} (hs : s âˆˆ ğ“¤ Î²)
    (hf : Pairwise fun x y => (f x, f y) âˆ‰ s) : @IsUniformEmbedding Î± Î² âŠ¥ â€¹_â€º f := by
  let _ : UniformSpace Î± := âŠ¥; have := discreteTopology_bot Î±
  exact IsUniformInducing.isUniformEmbedding âŸ¨comap_uniformity_of_spaced_out hs hfâŸ©

protected lemma IsUniformEmbedding.isEmbedding {f : Î± â†’ Î²} (h : IsUniformEmbedding f) :
    IsEmbedding f where
  toIsInducing := h.toIsUniformInducing.isInducing
  injective := h.injective

theorem IsUniformEmbedding.isDenseEmbedding {f : Î± â†’ Î²} (h : IsUniformEmbedding f)
    (hd : DenseRange f) : IsDenseEmbedding f :=
  { h.isEmbedding with dense := hd }

theorem isClosedEmbedding_of_spaced_out {Î±} [TopologicalSpace Î±] [DiscreteTopology Î±]
    [T0Space Î²] {f : Î± â†’ Î²} {s : Set (Î² Ã— Î²)} (hs : s âˆˆ ğ“¤ Î²)
    (hf : Pairwise fun x y => (f x, f y) âˆ‰ s) : IsClosedEmbedding f := by
  rcases @DiscreteTopology.eq_bot Î± _ _ with rfl; let _ : UniformSpace Î± := âŠ¥
  exact
    { (isUniformEmbedding_of_spaced_out hs hf).isEmbedding with
      isClosed_range := isClosed_range_of_spaced_out hs hf }

theorem closure_image_mem_nhds_of_isUniformInducing {s : Set (Î± Ã— Î±)} {e : Î± â†’ Î²} (b : Î²)
    (heâ‚ : IsUniformInducing e) (heâ‚‚ : IsDenseInducing e) (hs : s âˆˆ ğ“¤ Î±) :
    âˆƒ a, closure (e '' { a' | (a, a') âˆˆ s }) âˆˆ ğ“ b := by
  obtain âŸ¨U, âŸ¨hU, hUo, hsymmâŸ©, hsâŸ© :
    âˆƒ U, (U âˆˆ ğ“¤ Î² âˆ§ IsOpen U âˆ§ SetRel.IsSymm U) âˆ§ Prod.map e e â»Â¹' U âŠ† s := by
      rwa [â† heâ‚.comap_uniformity, (uniformity_hasBasis_open_symmetric.comap _).mem_iff] at hs
  rcases heâ‚‚.dense.mem_nhds (UniformSpace.ball_mem_nhds b hU) with âŸ¨a, haâŸ©
  refine âŸ¨a, mem_of_superset ?_ (closure_mono <| image_mono <| UniformSpace.ball_mono hs a)âŸ©
  have ho : IsOpen (UniformSpace.ball (e a) U) := UniformSpace.isOpen_ball (e a) hUo
  refine mem_of_superset (ho.mem_nhds <| UniformSpace.mem_ball_symmetry.2 ha) fun y hy => ?_
  refine mem_closure_iff_nhds.2 fun V hV => ?_
  rcases heâ‚‚.dense.mem_nhds (inter_mem hV (ho.mem_nhds hy)) with âŸ¨x, hxV, hxUâŸ©
  exact âŸ¨e x, hxV, mem_image_of_mem e hxUâŸ©

theorem isUniformEmbedding_subtypeEmb (p : Î± â†’ Prop) {e : Î± â†’ Î²} (ue : IsUniformEmbedding e)
    (de : IsDenseEmbedding e) : IsUniformEmbedding (IsDenseEmbedding.subtypeEmb p e) :=
  { comap_uniformity := by
      simp [comap_comap, Function.comp_def, IsDenseEmbedding.subtypeEmb, uniformity_subtype,
        ue.comap_uniformity.symm]
    injective := (de.subtype p).injective }

theorem IsUniformEmbedding.prod {Î±' : Type*} {Î²' : Type*} [UniformSpace Î±'] [UniformSpace Î²']
    {eâ‚ : Î± â†’ Î±'} {eâ‚‚ : Î² â†’ Î²'} (hâ‚ : IsUniformEmbedding eâ‚) (hâ‚‚ : IsUniformEmbedding eâ‚‚) :
    IsUniformEmbedding fun p : Î± Ã— Î² => (eâ‚ p.1, eâ‚‚ p.2) where
  toIsUniformInducing := hâ‚.isUniformInducing.prod hâ‚‚.isUniformInducing
  injective := hâ‚.injective.prodMap hâ‚‚.injective

/-- A set is complete iff its image under a uniform inducing map is complete. -/
theorem isComplete_image_iff {m : Î± â†’ Î²} {s : Set Î±} (hm : IsUniformInducing m) :
    IsComplete (m '' s) â†” IsComplete s := by
  have fact1 : SurjOn (map m) (Iic <| ğ“Ÿ s) (Iic <| ğ“Ÿ <| m '' s) := surjOn_image .. |>.filter_map_Iic
  have fact2 : MapsTo (map m) (Iic <| ğ“Ÿ s) (Iic <| ğ“Ÿ <| m '' s) := mapsTo_image .. |>.filter_map_Iic
  simp_rw [IsComplete, imp.swap (a := Cauchy _), â† mem_Iic (b := ğ“Ÿ _), fact1.forall fact2,
    hm.cauchy_map_iff, exists_mem_image, map_le_iff_le_comap, hm.isInducing.nhds_eq_comap]

/-- If `f : X â†’ Y` is an `IsUniformInducing` map, the image `f '' s` of a set `s` is complete
  if and only if `s` is complete. -/
theorem IsUniformInducing.isComplete_iff {f : Î± â†’ Î²} {s : Set Î±} (hf : IsUniformInducing f) :
    IsComplete (f '' s) â†” IsComplete s := isComplete_image_iff hf

/-- If `f : X â†’ Y` is an `IsUniformEmbedding`, the image `f '' s` of a set `s` is complete
  if and only if `s` is complete. -/
theorem IsUniformEmbedding.isComplete_iff {f : Î± â†’ Î²} {s : Set Î±} (hf : IsUniformEmbedding f) :
    IsComplete (f '' s) â†” IsComplete s := hf.isUniformInducing.isComplete_iff

/-- Sets of a subtype are complete iff their image under the coercion is complete. -/
theorem Subtype.isComplete_iff {p : Î± â†’ Prop} {s : Set { x // p x }} :
    IsComplete s â†” IsComplete ((â†‘) '' s : Set Î±) :=
  isUniformEmbedding_subtype_val.isComplete_iff.symm

alias âŸ¨isComplete_of_complete_image, _âŸ© := isComplete_image_iff

theorem completeSpace_iff_isComplete_range {f : Î± â†’ Î²} (hf : IsUniformInducing f) :
    CompleteSpace Î± â†” IsComplete (range f) := by
  rw [completeSpace_iff_isComplete_univ, â† isComplete_image_iff hf, image_univ]

alias âŸ¨_, IsUniformInducing.completeSpaceâŸ© := completeSpace_iff_isComplete_range

lemma IsUniformInducing.isComplete_range [CompleteSpace Î±] (hf : IsUniformInducing f) :
    IsComplete (range f) :=
  (completeSpace_iff_isComplete_range hf).1 â€¹_â€º

/-- If `f` is a surjective uniform inducing map,
then its domain is a complete space iff its codomain is a complete space.
See also `_root_.completeSpace_congr` for a version that assumes `f` to be an equivalence. -/
theorem IsUniformInducing.completeSpace_congr {f : Î± â†’ Î²} (hf : IsUniformInducing f)
    (hsurj : f.Surjective) : CompleteSpace Î± â†” CompleteSpace Î² := by
  rw [completeSpace_iff_isComplete_range hf, hsurj.range_eq, completeSpace_iff_isComplete_univ]

theorem SeparationQuotient.completeSpace_iff :
    CompleteSpace (SeparationQuotient Î±) â†” CompleteSpace Î± :=
  .symm <| isUniformInducing_mk.completeSpace_congr surjective_mk

instance SeparationQuotient.instCompleteSpace [CompleteSpace Î±] :
    CompleteSpace (SeparationQuotient Î±) :=
  completeSpace_iff.2 â€¹_â€º

/-- See also `IsUniformInducing.completeSpace_congr`
for a version that works for non-injective maps. -/
theorem completeSpace_congr {e : Î± â‰ƒ Î²} (he : IsUniformEmbedding e) :
    CompleteSpace Î± â†” CompleteSpace Î² :=
  he.completeSpace_congr e.surjective

theorem completeSpace_coe_iff_isComplete {s : Set Î±} : CompleteSpace s â†” IsComplete s := by
  rw [completeSpace_iff_isComplete_range isUniformEmbedding_subtype_val.isUniformInducing,
    Subtype.range_coe]

alias âŸ¨_, IsComplete.completeSpace_coeâŸ© := completeSpace_coe_iff_isComplete

instance IsClosed.completeSpace_coe [CompleteSpace Î±] {s : Set Î±} [hs : IsClosed s] :
    CompleteSpace s := hs.isComplete.completeSpace_coe

theorem completeSpace_ulift_iff : CompleteSpace (ULift Î±) â†” CompleteSpace Î± :=
  IsUniformInducing.completeSpace_congr âŸ¨rflâŸ© ULift.down_surjective

/-- The lift of a complete space to another universe is still complete. -/
instance ULift.instCompleteSpace [CompleteSpace Î±] : CompleteSpace (ULift Î±) :=
  completeSpace_ulift_iff.2 â€¹_â€º

theorem completeSpace_extension {m : Î² â†’ Î±} (hm : IsUniformInducing m) (dense : DenseRange m)
    (h : âˆ€ f : Filter Î², Cauchy f â†’ âˆƒ x : Î±, map m f â‰¤ ğ“ x) : CompleteSpace Î± :=
  âŸ¨fun {f : Filter Î±} (hf : Cauchy f) =>
    let p : Set (Î± Ã— Î±) â†’ Set Î± â†’ Set Î± := fun s t => { y : Î± | âˆƒ x : Î±, x âˆˆ t âˆ§ (x, y) âˆˆ s }
    let g := (ğ“¤ Î±).lift fun s => f.lift' (p s)
    have mpâ‚€ : Monotone p := fun _ _ h _ _ âŸ¨x, xs, xaâŸ© => âŸ¨x, xs, h xaâŸ©
    have mpâ‚ : âˆ€ {s}, Monotone (p s) := fun h _ âŸ¨y, ya, yxsâŸ© => âŸ¨y, h ya, yxsâŸ©
    have : f â‰¤ g := le_iInfâ‚‚ fun _ hs => le_iInfâ‚‚ fun _ ht =>
      le_principal_iff.mpr <| mem_of_superset ht fun x hx => âŸ¨x, hx, refl_mem_uniformity hsâŸ©
    have : NeBot g := hf.left.mono this
    have : NeBot (comap m g) :=
      comap_neBot fun _ ht =>
        let âŸ¨t', ht', ht_memâŸ© := (mem_lift_sets <| monotone_lift' monotone_const mpâ‚€).mp ht
        let âŸ¨_, ht'', ht'_subâŸ© := (mem_lift'_sets mpâ‚).mp ht_mem
        let âŸ¨x, hxâŸ© := hf.left.nonempty_of_mem ht''
        have hâ‚€ : NeBot (ğ“[range m] x) := dense.nhdsWithin_neBot x
        have hâ‚ : { y | (x, y) âˆˆ t' } âˆˆ ğ“[range m] x :=
          @mem_inf_of_left Î± (ğ“ x) (ğ“Ÿ (range m)) _ <| mem_nhds_left x ht'
        have hâ‚‚ : range m âˆˆ ğ“[range m] x :=
          @mem_inf_of_right Î± (ğ“ x) (ğ“Ÿ (range m)) _ <| Subset.refl _
        have : { y | (x, y) âˆˆ t' } âˆ© range m âˆˆ ğ“[range m] x := @inter_mem Î± (ğ“[range m] x) _ _ hâ‚ hâ‚‚
        let âŸ¨_, xyt', b, b_eqâŸ© := hâ‚€.nonempty_of_mem this
        âŸ¨b, b_eq.symm â–¸ ht'_sub âŸ¨x, hx, xyt'âŸ©âŸ©
    have : Cauchy g :=
      âŸ¨â€¹NeBot gâ€º, fun _ hs =>
        let âŸ¨sâ‚, hsâ‚, comp_sâ‚âŸ© := comp_mem_uniformity_sets hs
        let âŸ¨sâ‚‚, hsâ‚‚, comp_sâ‚‚âŸ© := comp_mem_uniformity_sets hsâ‚
        let âŸ¨t, ht, (prod_t : t Ã—Ë¢ t âŠ† sâ‚‚)âŸ© := mem_prod_same_iff.mp (hf.right hsâ‚‚)
        have hgâ‚ : p (preimage Prod.swap sâ‚) t âˆˆ g :=
          mem_lift (symm_le_uniformity hsâ‚) <| @mem_lift' Î± Î± f _ t ht
        have hgâ‚‚ : p sâ‚‚ t âˆˆ g := mem_lift hsâ‚‚ <| @mem_lift' Î± Î± f _ t ht
        have hg : p (Prod.swap â»Â¹' sâ‚) t Ã—Ë¢ p sâ‚‚ t âˆˆ g Ã—Ë¢ g := @prod_mem_prod Î± Î± _ _ g g hgâ‚ hgâ‚‚
        (g Ã—Ë¢ g).sets_of_superset hg fun âŸ¨_, _âŸ© âŸ¨âŸ¨câ‚, câ‚t, hcâ‚âŸ©, âŸ¨câ‚‚, câ‚‚t, hcâ‚‚âŸ©âŸ© =>
          have : (câ‚, câ‚‚) âˆˆ t Ã—Ë¢ t := âŸ¨câ‚t, câ‚‚tâŸ©
          comp_sâ‚ <| SetRel.prodMk_mem_comp hcâ‚ <| comp_sâ‚‚ <|
            SetRel.prodMk_mem_comp (prod_t this) hcâ‚‚âŸ©
    have : Cauchy (Filter.comap m g) := â€¹Cauchy gâ€º.comap' (le_of_eq hm.comap_uniformity) â€¹_â€º
    let âŸ¨x, (hx : map m (Filter.comap m g) â‰¤ ğ“ x)âŸ© := h _ this
    have : ClusterPt x (map m (Filter.comap m g)) :=
      (le_nhds_iff_adhp_of_cauchy (this.map hm.uniformContinuous)).mp hx
    have : ClusterPt x g := this.mono map_comap_le
    âŸ¨x,
      calc
        f â‰¤ g := by assumption
        _ â‰¤ ğ“ x := le_nhds_of_cauchy_adhp â€¹Cauchy gâ€º this
        âŸ©âŸ©

lemma totallyBounded_image_iff {f : Î± â†’ Î²} {s : Set Î±} (hf : IsUniformInducing f) :
    TotallyBounded (f '' s) â†” TotallyBounded s := by
  refine âŸ¨fun hs â†¦ ?_, fun h â†¦ h.image hf.uniformContinuousâŸ©
  simp_rw [(hf.basis_uniformity (basis_sets _)).totallyBounded_iff]
  intro t ht
  rcases exists_subset_image_finite_and.1 (hs.exists_subset ht) with âŸ¨u, -, hfin, hâŸ©
  use u, hfin
  rwa [biUnion_image, image_subset_iff, preimage_iUnionâ‚‚] at h

theorem totallyBounded_preimage {f : Î± â†’ Î²} {s : Set Î²} (hf : IsUniformInducing f)
    (hs : TotallyBounded s) : TotallyBounded (f â»Â¹' s) :=
  (totallyBounded_image_iff hf).1 <| hs.subset <| image_preimage_subset ..

instance CompleteSpace.sum [CompleteSpace Î±] [CompleteSpace Î²] : CompleteSpace (Î± âŠ• Î²) := by
  rw [completeSpace_iff_isComplete_univ, â† range_inl_union_range_inr]
  exact isUniformEmbedding_inl.isUniformInducing.isComplete_range.union
    isUniformEmbedding_inr.isUniformInducing.isComplete_range

end

theorem isUniformEmbedding_comap {Î± : Type*} {Î² : Type*} {f : Î± â†’ Î²} [u : UniformSpace Î²]
    (hf : Function.Injective f) : @IsUniformEmbedding Î± Î² (UniformSpace.comap f u) u f :=
  @IsUniformEmbedding.mk _ _ (UniformSpace.comap f u) _ _
    (@IsUniformInducing.mk _ _ (UniformSpace.comap f u) _ _ rfl) hf

/-- Pull back a uniform space structure by an embedding, adjusting the new uniform structure to
make sure that its topology is defeq to the original one. -/
def Topology.IsEmbedding.comapUniformSpace {Î± Î²} [TopologicalSpace Î±] [u : UniformSpace Î²]
    (f : Î± â†’ Î²) (h : IsEmbedding f) : UniformSpace Î± :=
  (u.comap f).replaceTopology h.eq_induced

theorem Embedding.to_isUniformEmbedding {Î± Î²} [TopologicalSpace Î±] [u : UniformSpace Î²] (f : Î± â†’ Î²)
    (h : IsEmbedding f) : @IsUniformEmbedding Î± Î² (h.comapUniformSpace f) u f :=
  let _ := h.comapUniformSpace f
  { comap_uniformity := rfl
    injective := h.injective }

section UniformExtension

variable {Î± : Type*} {Î² : Type*} {Î³ : Type*} [UniformSpace Î±] [UniformSpace Î²] [UniformSpace Î³]
  {e : Î² â†’ Î±} (h_e : IsUniformInducing e) (h_dense : DenseRange e) {f : Î² â†’ Î³}
  (h_f : UniformContinuous f)

local notation "Ïˆ" => IsDenseInducing.extend (IsUniformInducing.isDenseInducing h_e h_dense) f

include h_e h_dense h_f in
theorem uniformly_extend_exists [CompleteSpace Î³] (a : Î±) : âˆƒ c, Tendsto f (comap e (ğ“ a)) (ğ“ c) :=
  let de := h_e.isDenseInducing h_dense
  have : Cauchy (ğ“ a) := cauchy_nhds
  have : Cauchy (comap e (ğ“ a)) :=
    this.comap' (le_of_eq h_e.comap_uniformity) (de.comap_nhds_neBot _)
  have : Cauchy (map f (comap e (ğ“ a))) := this.map h_f
  CompleteSpace.complete this

theorem uniform_extend_subtype [CompleteSpace Î³] {p : Î± â†’ Prop} {e : Î± â†’ Î²} {f : Î± â†’ Î³} {b : Î²}
    {s : Set Î±} (hf : UniformContinuous fun x : Subtype p => f x.val) (he : IsUniformEmbedding e)
    (hd : âˆ€ x : Î², x âˆˆ closure (range e)) (hb : closure (e '' s) âˆˆ ğ“ b) (hs : IsClosed s)
    (hp : âˆ€ x âˆˆ s, p x) : âˆƒ c, Tendsto f (comap e (ğ“ b)) (ğ“ c) := by
  have de : IsDenseEmbedding e := he.isDenseEmbedding hd
  have de' : IsDenseEmbedding (IsDenseEmbedding.subtypeEmb p e) := de.subtype p
  have ue' : IsUniformEmbedding (IsDenseEmbedding.subtypeEmb p e) :=
    isUniformEmbedding_subtypeEmb _ he de
  have : b âˆˆ closure (e '' { x | p x }) :=
    (closure_mono <| monotone_image <| hp) (mem_of_mem_nhds hb)
  let âŸ¨c, hcâŸ© := uniformly_extend_exists ue'.isUniformInducing de'.dense hf âŸ¨b, thisâŸ©
  replace hc : Tendsto (f âˆ˜ Subtype.val (p := p)) (((ğ“ b).comap e).comap Subtype.val) (ğ“ c) := by
    simpa only [nhds_subtype_eq_comap, comap_comap, IsDenseEmbedding.subtypeEmb_coe] using hc
  refine âŸ¨c, (tendsto_comap'_iff ?_).1 hcâŸ©
  rw [Subtype.range_coe_subtype]
  exact âŸ¨_, hb, by rwa [â† de.isInducing.closure_eq_preimage_closure_image, hs.closure_eq]âŸ©

include h_e h_f in
theorem uniformly_extend_spec [CompleteSpace Î³] (a : Î±) : Tendsto f (comap e (ğ“ a)) (ğ“ (Ïˆ a)) := by
  simpa only [IsDenseInducing.extend] using
    tendsto_nhds_limUnder (uniformly_extend_exists h_e â€¹_â€º h_f _)

include h_f in
theorem uniformContinuous_uniformly_extend [CompleteSpace Î³] : UniformContinuous Ïˆ := fun d hd =>
  let âŸ¨s, hs, hs_compâŸ© := comp3_mem_uniformity hd
  have h_pnt : âˆ€ {a m}, m âˆˆ ğ“ a â†’ âˆƒ c âˆˆ f '' (e â»Â¹' m), (c, Ïˆ a) âˆˆ s âˆ§ (Ïˆ a, c) âˆˆ s :=
    fun {a m} hm =>
    have nb : NeBot (map f (comap e (ğ“ a))) :=
      ((h_e.isDenseInducing h_dense).comap_nhds_neBot _).map _
    have :
      f '' (e â»Â¹' m) âˆ© ({ c | (c, Ïˆ a) âˆˆ s } âˆ© { c | (Ïˆ a, c) âˆˆ s }) âˆˆ map f (comap e (ğ“ a)) :=
      inter_mem (image_mem_map <| preimage_mem_comap <| hm)
        (uniformly_extend_spec h_e h_dense h_f _
          (inter_mem (mem_nhds_right _ hs) (mem_nhds_left _ hs)))
    nb.nonempty_of_mem this
  have : (Prod.map f f) â»Â¹' s âˆˆ ğ“¤ Î² := h_f hs
  have : (Prod.map f f) â»Â¹' s âˆˆ comap (Prod.map e e) (ğ“¤ Î±) := by
    rwa [â† h_e.comap_uniformity] at this
  let âŸ¨t, ht, tsâŸ© := this
  show (Prod.map Ïˆ Ïˆ) â»Â¹' d âˆˆ ğ“¤ Î± from
    mem_of_superset (interior_mem_uniformity ht) fun âŸ¨xâ‚, xâ‚‚âŸ© hx_t => by
      have : interior t âˆˆ ğ“ (xâ‚, xâ‚‚) := isOpen_interior.mem_nhds hx_t
      let âŸ¨mâ‚, hmâ‚, mâ‚‚, hmâ‚‚, (hm : mâ‚ Ã—Ë¢ mâ‚‚ âŠ† interior t)âŸ© := mem_nhds_prod_iff.mp this
      obtain âŸ¨_, âŸ¨a, haâ‚, rflâŸ©, _, haâ‚‚âŸ© := h_pnt hmâ‚
      obtain âŸ¨_, âŸ¨b, hbâ‚, rflâŸ©, hbâ‚‚, _âŸ© := h_pnt hmâ‚‚
      have : Prod.map f f (a, b) âˆˆ s :=
        ts <| mem_preimage.2 <| interior_subset (@hm (e a, e b) âŸ¨haâ‚, hbâ‚âŸ©)
      exact hs_comp âŸ¨f a, haâ‚‚, âŸ¨f b, this, hbâ‚‚âŸ©âŸ©

variable [T0Space Î³]

include h_f in
theorem uniformly_extend_of_ind (b : Î²) : Ïˆ (e b) = f b :=
  IsDenseInducing.extend_eq_at _ h_f.continuous.continuousAt

theorem uniformly_extend_unique {g : Î± â†’ Î³} (hg : âˆ€ b, g (e b) = f b) (hc : Continuous g) : Ïˆ = g :=
  IsDenseInducing.extend_unique _ hg hc

end UniformExtension

section DenseExtension

variable {Î± Î² : Type*} [UniformSpace Î±] [UniformSpace Î²]

theorem isUniformInducing_val (s : Set Î±) :
    IsUniformInducing (@Subtype.val Î± s) := âŸ¨uniformity_setCoeâŸ©

@[simp]
theorem uniformContinuous_rangeFactorization_iff {f : Î± â†’ Î²} :
    UniformContinuous (rangeFactorization f) â†” UniformContinuous f :=
  (isUniformInducing_val _).uniformContinuous_iff

theorem UniformContinuous.rangeFactorization {f : Î± â†’ Î²} (hf : UniformContinuous f) :
    UniformContinuous (rangeFactorization f) :=
  uniformContinuous_rangeFactorization_iff.mpr hf

@[simp]
theorem isUniformInducing_rangeFactorization_iff {f : Î± â†’ Î²} :
    IsUniformInducing (rangeFactorization f) â†” IsUniformInducing f :=
  (isUniformInducing_val (range f)).isUniformInducing_comp_iff.symm

theorem IsUniformInducing.rangeFactorization {f : Î± â†’ Î²} (hf : IsUniformInducing f) :
    IsUniformInducing (rangeFactorization f) :=
  isUniformInducing_rangeFactorization_iff.2 hf

namespace Dense

variable {s : Set Î±} {f : s â†’ Î²}

theorem extend_exists [CompleteSpace Î²] (hs : Dense s) (hf : UniformContinuous f) (a : Î±) :
    âˆƒ b, Tendsto f (comap (â†‘) (ğ“ a)) (ğ“ b) :=
  uniformly_extend_exists (isUniformInducing_val s) hs.denseRange_val hf a

theorem extend_spec [CompleteSpace Î²] (hs : Dense s) (hf : UniformContinuous f) (a : Î±) :
    Tendsto f (comap (â†‘) (ğ“ a)) (ğ“ (hs.extend f a)) :=
  uniformly_extend_spec (isUniformInducing_val s) hs.denseRange_val hf a

theorem uniformContinuous_extend [CompleteSpace Î²] (hs : Dense s) (hf : UniformContinuous f) :
    UniformContinuous (hs.extend f) :=
  uniformContinuous_uniformly_extend (isUniformInducing_val s) hs.denseRange_val hf

variable [T0Space Î²]

theorem extend_of_ind (hs : Dense s) (hf : UniformContinuous f) (x : s) :
    hs.extend f x = f x :=
  IsDenseInducing.extend_eq_at _ hf.continuous.continuousAt

end Dense

lemma IsDenseInducing.isUniformInducing_extend {Î³ : Type*} [UniformSpace Î³]
    [CompleteSpace Î²] [CompleteSpace Î³] {i : Î± â†’ Î²} {f : Î± â†’ Î³}
    (hid : IsDenseInducing i) (hi : IsUniformInducing i) (h : IsUniformInducing f) :
    IsUniformInducing (hid.extend f) := by
  let sf := SeparationQuotient.mk âˆ˜ f
  have : CompleteSpace (closure (range sf)) :=
    isClosed_closure.isComplete.completeSpace_coe
  let ff : Î± â†’ closure (range sf) := inclusion subset_closure âˆ˜ rangeFactorization sf
  have hgu : IsUniformInducing ff :=
    (isUniformEmbedding_set_inclusion subset_closure).isUniformInducing.comp
      (SeparationQuotient.isUniformInducing_mk.comp h).rangeFactorization
  have hgd : DenseRange ff :=
    ((denseRange_inclusion_iff subset_closure).2 subset_rfl).comp
      rangeFactorization_surjective.denseRange (continuous_inclusion subset_closure)
  have hg : IsDenseInducing ff := hgu.isDenseInducing hgd
  let fwd := hid.extend ff
  have hfwd : UniformContinuous fwd :=
    uniformContinuous_uniformly_extend hi hid.dense hgu.uniformContinuous
  have hg' : UniformContinuous (hg.extend i) :=
    uniformContinuous_uniformly_extend hgu hgd hi.uniformContinuous
  have key : SeparationQuotient.mk âˆ˜ hg.extend i âˆ˜ fwd = SeparationQuotient.mk := by
    ext x
    induction x using isClosed_property hid.dense
    Â· exact isClosed_eq (SeparationQuotient.continuous_mk.comp (hg'.comp hfwd).continuous)
        SeparationQuotient.continuous_mk
    Â· simpa [fwd, hid.extend_eq hgu.uniformContinuous.continuous]
        using hg.inseparable_extend hi.uniformContinuous.continuous.continuousAt
  have hfu : IsUniformInducing fwd := by
    refine IsUniformInducing.of_comp hfwd (SeparationQuotient.uniformContinuous_mk.comp hg') ?_
    rw [Function.comp_assoc, key]
    exact SeparationQuotient.isUniformInducing_mk
  have hrr : range (SeparationQuotient.mk âˆ˜ hid.extend f) âŠ†
      closure (range (SeparationQuotient.mk âˆ˜ f)) := by
    refine ((SeparationQuotient.continuous_mk.comp (uniformContinuous_uniformly_extend hi hid.dense
      h.uniformContinuous).continuous).range_subset_closure_image_dense hid.dense).trans
      (closure_mono (subset_of_eq ?_))
    rw [â† range_comp]
    apply congrArg range
    funext x
    simpa using (hid.inseparable_extend h.uniformContinuous.continuous.continuousAt)
  suffices Subtype.val âˆ˜ fwd = SeparationQuotient.mk âˆ˜ hid.extend f by
    rw [â† SeparationQuotient.isUniformInducing_mk.isUniformInducing_comp_iff, â† this]
    exact (isUniformInducing_val _).comp hfu
  rw [â† coe_comp_rangeFactorization (SeparationQuotient.mk âˆ˜ hid.extend f),
    â† val_comp_inclusion hrr, Function.comp_assoc, Subtype.val_injective.comp_left.eq_iff]
  refine hid.extend_unique ?_ ?_
  Â· simp [ff, hid.inseparable_extend h.uniformContinuous.continuous.continuousAt, sf]
  Â· exact (continuous_inclusion hrr).comp
      (SeparationQuotient.continuous_mk.comp (uniformContinuous_uniformly_extend hi hid.dense
        h.uniformContinuous).continuous).rangeFactorization

end DenseExtension
