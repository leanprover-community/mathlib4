/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, S√©bastien Gou√´zel, Patrick Massot
-/
import Mathlib.Topology.UniformSpace.Cauchy
import Mathlib.Topology.UniformSpace.Separation
import Mathlib.Topology.DenseEmbedding

/-!
# Uniform embeddings of uniform spaces.

Extension of uniform continuous functions.
-/


open Filter Function Set Uniformity Topology

section

universe u v w
variable {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w} [UniformSpace Œ±] [UniformSpace Œ≤] [UniformSpace Œ≥]
  {f : Œ± ‚Üí Œ≤}

/-!
### Uniform inducing maps
-/

/-- A map `f : Œ± ‚Üí Œ≤` between uniform spaces is called *uniform inducing* if the uniformity filter
on `Œ±` is the pullback of the uniformity filter on `Œ≤` under `Prod.map f f`. If `Œ±` is a separated
space, then this implies that `f` is injective, hence it is a `IsUniformEmbedding`. -/
@[mk_iff]
structure IsUniformInducing (f : Œ± ‚Üí Œ≤) : Prop where
  /-- The uniformity filter on the domain is the pullback of the uniformity filter on the codomain
  under `Prod.map f f`. -/
  comap_uniformity : comap (fun x : Œ± √ó Œ± => (f x.1, f x.2)) (ùì§ Œ≤) = ùì§ Œ±

@[deprecated (since := "2024-10-08")] alias UniformInducing := IsUniformInducing

lemma isUniformInducing_iff_uniformSpace {f : Œ± ‚Üí Œ≤} :
    IsUniformInducing f ‚Üî ‚ÄπUniformSpace Œ≤‚Ä∫.comap f = ‚ÄπUniformSpace Œ±‚Ä∫ := by
  rw [isUniformInducing_iff, UniformSpace.ext_iff, Filter.ext_iff]
  rfl

@[deprecated (since := "2024-10-05")]
alias uniformInducing_iff_uniformSpace := isUniformInducing_iff_uniformSpace

protected alias ‚ü®IsUniformInducing.comap_uniformSpace, _‚ü© := isUniformInducing_iff_uniformSpace

@[deprecated (since := "2024-10-08")] alias UniformInducing.comap_uniformSpace :=
  IsUniformInducing.comap_uniformSpace

lemma isUniformInducing_iff' {f : Œ± ‚Üí Œ≤} :
    IsUniformInducing f ‚Üî UniformContinuous f ‚àß comap (Prod.map f f) (ùì§ Œ≤) ‚â§ ùì§ Œ± := by
  rw [isUniformInducing_iff, UniformContinuous, tendsto_iff_comap, le_antisymm_iff, and_comm]; rfl

@[deprecated (since := "2024-10-05")]
alias uniformInducing_iff' := isUniformInducing_iff'

protected lemma Filter.HasBasis.isUniformInducing_iff {Œπ Œπ'} {p : Œπ ‚Üí Prop} {p' : Œπ' ‚Üí Prop} {s s'}
    (h : (ùì§ Œ±).HasBasis p s) (h' : (ùì§ Œ≤).HasBasis p' s') {f : Œ± ‚Üí Œ≤} :
    IsUniformInducing f ‚Üî
      (‚àÄ i, p' i ‚Üí ‚àÉ j, p j ‚àß ‚àÄ x y, (x, y) ‚àà s j ‚Üí (f x, f y) ‚àà s' i) ‚àß
        (‚àÄ j, p j ‚Üí ‚àÉ i, p' i ‚àß ‚àÄ x y, (f x, f y) ‚àà s' i ‚Üí (x, y) ‚àà s j) := by
  simp [isUniformInducing_iff', h.uniformContinuous_iff h', (h'.comap _).le_basis_iff h, subset_def]

@[deprecated (since := "2024-10-05")]
alias Filter.HasBasis.uniformInducing_iff := Filter.HasBasis.isUniformInducing_iff

theorem IsUniformInducing.mk' {f : Œ± ‚Üí Œ≤}
    (h : ‚àÄ s, s ‚àà ùì§ Œ± ‚Üî ‚àÉ t ‚àà ùì§ Œ≤, ‚àÄ x y : Œ±, (f x, f y) ‚àà t ‚Üí (x, y) ‚àà s) : IsUniformInducing f :=
  ‚ü®by simp [eq_comm, Filter.ext_iff, subset_def, h]‚ü©

@[deprecated (since := "2024-10-05")]
alias UniformInducing.mk' := IsUniformInducing.mk'

theorem IsUniformInducing.id : IsUniformInducing (@id Œ±) :=
  ‚ü®by rw [‚Üê Prod.map_def, Prod.map_id, comap_id]‚ü©

@[deprecated (since := "2024-10-05")]
alias uniformInducing_id := IsUniformInducing.id

theorem IsUniformInducing.comp {g : Œ≤ ‚Üí Œ≥} (hg : IsUniformInducing g) {f : Œ± ‚Üí Œ≤}
    (hf : IsUniformInducing f) : IsUniformInducing (g ‚àò f) :=
  ‚ü®by rw [‚Üê hf.1, ‚Üê hg.1, comap_comap]; rfl‚ü©

@[deprecated (since := "2024-10-05")]
alias UniformInducing.comp := IsUniformInducing.comp

theorem IsUniformInducing.of_comp_iff {g : Œ≤ ‚Üí Œ≥} (hg : IsUniformInducing g) {f : Œ± ‚Üí Œ≤} :
    IsUniformInducing (g ‚àò f) ‚Üî IsUniformInducing f := by
  refine ‚ü®fun h ‚Ü¶ ?_, hg.comp‚ü©
  rw [isUniformInducing_iff, ‚Üê hg.comap_uniformity, comap_comap, ‚Üê h.comap_uniformity,
    Function.comp_def, Function.comp_def]

@[deprecated (since := "2024-10-05")]
alias UniformInducing.of_comp_iff := IsUniformInducing.of_comp_iff

theorem IsUniformInducing.basis_uniformity {f : Œ± ‚Üí Œ≤} (hf : IsUniformInducing f) {Œπ : Sort*}
    {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí Set (Œ≤ √ó Œ≤)} (H : (ùì§ Œ≤).HasBasis p s) :
    (ùì§ Œ±).HasBasis p fun i => Prod.map f f ‚Åª¬π' s i :=
  hf.1 ‚ñ∏ H.comap _

@[deprecated (since := "2024-10-05")]
alias UniformInducing.basis_uniformity := IsUniformInducing.basis_uniformity

theorem IsUniformInducing.cauchy_map_iff {f : Œ± ‚Üí Œ≤} (hf : IsUniformInducing f) {F : Filter Œ±} :
    Cauchy (map f F) ‚Üî Cauchy F := by
  simp only [Cauchy, map_neBot_iff, prod_map_map_eq, map_le_iff_le_comap, ‚Üê hf.comap_uniformity]

@[deprecated (since := "2024-10-05")]
alias UniformInducing.cauchy_map_iff := IsUniformInducing.cauchy_map_iff

theorem IsUniformInducing.of_comp {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ≥} (hf : UniformContinuous f)
    (hg : UniformContinuous g) (hgf : IsUniformInducing (g ‚àò f)) : IsUniformInducing f := by
  refine ‚ü®le_antisymm ?_ hf.le_comap‚ü©
  rw [‚Üê hgf.1, ‚Üê Prod.map_def, ‚Üê Prod.map_def, ‚Üê Prod.map_comp_map f f g g, ‚Üê comap_comap]
  exact comap_mono hg.le_comap

@[deprecated (since := "2024-10-05")]
alias uniformInducing_of_compose := IsUniformInducing.of_comp

theorem IsUniformInducing.uniformContinuous {f : Œ± ‚Üí Œ≤} (hf : IsUniformInducing f) :
    UniformContinuous f := (isUniformInducing_iff'.1 hf).1

@[deprecated (since := "2024-10-05")]
alias UniformInducing.uniformContinuous := IsUniformInducing.uniformContinuous

theorem IsUniformInducing.uniformContinuous_iff {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ≥} (hg : IsUniformInducing g) :
    UniformContinuous f ‚Üî UniformContinuous (g ‚àò f) := by
  dsimp only [UniformContinuous, Tendsto]
  simp only [‚Üê hg.comap_uniformity, ‚Üê map_le_iff_le_comap, Filter.map_map, Function.comp_def]

@[deprecated (since := "2024-10-05")]
alias UniformInducing.uniformContinuous_iff := IsUniformInducing.uniformContinuous_iff

protected theorem IsUniformInducing.isUniformInducing_comp_iff {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ≥}
    (hg : IsUniformInducing g) : IsUniformInducing (g ‚àò f) ‚Üî IsUniformInducing f := by
  simp only [isUniformInducing_iff, ‚Üê hg.comap_uniformity, comap_comap, Function.comp_def]

@[deprecated (since := "2024-10-05")]
alias UniformInducing.uniformInducing_comp_iff := IsUniformInducing.isUniformInducing_comp_iff

theorem IsUniformInducing.uniformContinuousOn_iff {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ≥} {S : Set Œ±}
    (hg : IsUniformInducing g) :
    UniformContinuousOn f S ‚Üî UniformContinuousOn (g ‚àò f) S := by
  dsimp only [UniformContinuousOn, Tendsto]
  rw [‚Üê hg.comap_uniformity, ‚Üê map_le_iff_le_comap, Filter.map_map, comp_def, comp_def]

@[deprecated (since := "2024-10-05")]
alias UniformInducing.uniformContinuousOn_iff := IsUniformInducing.uniformContinuousOn_iff

theorem IsUniformInducing.isInducing {f : Œ± ‚Üí Œ≤} (h : IsUniformInducing f) : IsInducing f := by
  obtain rfl := h.comap_uniformSpace
  exact .induced f

@[deprecated (since := "2024-10-28")]
alias IsUniformInducing.inducing := IsUniformInducing.isInducing

@[deprecated (since := "2024-10-05")]
alias UniformInducing.isInducing := IsUniformInducing.isInducing

@[deprecated (since := "2024-10-28")] alias UniformInducing.inducing := UniformInducing.isInducing

theorem IsUniformInducing.prod {Œ±' : Type*} {Œ≤' : Type*} [UniformSpace Œ±'] [UniformSpace Œ≤']
    {e‚ÇÅ : Œ± ‚Üí Œ±'} {e‚ÇÇ : Œ≤ ‚Üí Œ≤'} (h‚ÇÅ : IsUniformInducing e‚ÇÅ) (h‚ÇÇ : IsUniformInducing e‚ÇÇ) :
    IsUniformInducing fun p : Œ± √ó Œ≤ => (e‚ÇÅ p.1, e‚ÇÇ p.2) :=
  ‚ü®by simp [Function.comp_def, uniformity_prod, ‚Üê h‚ÇÅ.1, ‚Üê h‚ÇÇ.1, comap_inf, comap_comap]‚ü©

@[deprecated (since := "2024-10-05")]
alias UniformInducing.prod := IsUniformInducing.prod

lemma IsUniformInducing.isDenseInducing (h : IsUniformInducing f) (hd : DenseRange f) :
    IsDenseInducing f where
  toIsInducing := h.isInducing
  dense := hd

@[deprecated (since := "2024-10-05")]
alias UniformInducing.isDenseInducing := IsUniformInducing.isDenseInducing

lemma SeparationQuotient.isUniformInducing_mk :
    IsUniformInducing (mk : Œ± ‚Üí SeparationQuotient Œ±) :=
  ‚ü®comap_mk_uniformity‚ü©

@[deprecated (since := "2024-10-05")]
alias SeparationQuotient.uniformInducing_mk := SeparationQuotient.isUniformInducing_mk

protected theorem IsUniformInducing.injective [T0Space Œ±] {f : Œ± ‚Üí Œ≤} (h : IsUniformInducing f) :
    Injective f :=
  h.isInducing.injective

@[deprecated (since := "2024-10-05")]
alias UniformInducing.injective := IsUniformInducing.injective

/-!
### Uniform embeddings
-/

/-- A map `f : Œ± ‚Üí Œ≤` between uniform spaces is a *uniform embedding* if it is uniform inducing and
injective. If `Œ±` is a separated space, then the latter assumption follows from the former. -/
@[mk_iff]
structure IsUniformEmbedding (f : Œ± ‚Üí Œ≤) : Prop extends IsUniformInducing f where
  /-- A uniform embedding is injective. -/
  injective : Function.Injective f

lemma IsUniformEmbedding.isUniformInducing (hf : IsUniformEmbedding f) : IsUniformInducing f :=
  hf.toIsUniformInducing

@[deprecated (since := "2024-10-03")] alias UniformEmbedding := IsUniformEmbedding

theorem isUniformEmbedding_iff' {f : Œ± ‚Üí Œ≤} :
    IsUniformEmbedding f ‚Üî
      Injective f ‚àß UniformContinuous f ‚àß comap (Prod.map f f) (ùì§ Œ≤) ‚â§ ùì§ Œ± := by
  rw [isUniformEmbedding_iff, and_comm, isUniformInducing_iff']

@[deprecated (since := "2024-10-01")]
alias uniformEmbedding_iff' := isUniformEmbedding_iff'

theorem Filter.HasBasis.isUniformEmbedding_iff' {Œπ Œπ'} {p : Œπ ‚Üí Prop} {p' : Œπ' ‚Üí Prop} {s s'}
    (h : (ùì§ Œ±).HasBasis p s) (h' : (ùì§ Œ≤).HasBasis p' s') {f : Œ± ‚Üí Œ≤} :
    IsUniformEmbedding f ‚Üî Injective f ‚àß
      (‚àÄ i, p' i ‚Üí ‚àÉ j, p j ‚àß ‚àÄ x y, (x, y) ‚àà s j ‚Üí (f x, f y) ‚àà s' i) ‚àß
        (‚àÄ j, p j ‚Üí ‚àÉ i, p' i ‚àß ‚àÄ x y, (f x, f y) ‚àà s' i ‚Üí (x, y) ‚àà s j) := by
  rw [isUniformEmbedding_iff, and_comm, h.isUniformInducing_iff h']

@[deprecated (since := "2024-10-01")]
alias Filter.HasBasis.uniformEmbedding_iff' := Filter.HasBasis.isUniformEmbedding_iff'

theorem Filter.HasBasis.isUniformEmbedding_iff {Œπ Œπ'} {p : Œπ ‚Üí Prop} {p' : Œπ' ‚Üí Prop} {s s'}
    (h : (ùì§ Œ±).HasBasis p s) (h' : (ùì§ Œ≤).HasBasis p' s') {f : Œ± ‚Üí Œ≤} :
    IsUniformEmbedding f ‚Üî Injective f ‚àß UniformContinuous f ‚àß
      (‚àÄ j, p j ‚Üí ‚àÉ i, p' i ‚àß ‚àÄ x y, (f x, f y) ‚àà s' i ‚Üí (x, y) ‚àà s j) := by
  simp only [h.isUniformEmbedding_iff' h', h.uniformContinuous_iff h']

@[deprecated (since := "2024-10-01")]
alias Filter.HasBasis.uniformEmbedding_iff := Filter.HasBasis.isUniformEmbedding_iff

theorem isUniformEmbedding_subtype_val {p : Œ± ‚Üí Prop} :
    IsUniformEmbedding (Subtype.val : Subtype p ‚Üí Œ±) :=
  { comap_uniformity := rfl
    injective := Subtype.val_injective }

@[deprecated (since := "2024-10-01")]
alias uniformEmbedding_subtype_val := isUniformEmbedding_subtype_val

theorem isUniformEmbedding_set_inclusion {s t : Set Œ±} (hst : s ‚äÜ t) :
    IsUniformEmbedding (inclusion hst) where
  comap_uniformity := by rw [uniformity_subtype, uniformity_subtype, comap_comap]; rfl
  injective := inclusion_injective hst

@[deprecated (since := "2024-10-01")]
alias uniformEmbedding_set_inclusion := isUniformEmbedding_set_inclusion

theorem IsUniformEmbedding.comp {g : Œ≤ ‚Üí Œ≥} (hg : IsUniformEmbedding g) {f : Œ± ‚Üí Œ≤}
    (hf : IsUniformEmbedding f) : IsUniformEmbedding (g ‚àò f) where
  toIsUniformInducing := hg.isUniformInducing.comp hf.isUniformInducing
  injective := hg.injective.comp hf.injective

@[deprecated (since := "2024-10-01")]
alias UniformEmbedding.comp := IsUniformEmbedding.comp

theorem IsUniformEmbedding.of_comp_iff {g : Œ≤ ‚Üí Œ≥} (hg : IsUniformEmbedding g) {f : Œ± ‚Üí Œ≤} :
    IsUniformEmbedding (g ‚àò f) ‚Üî IsUniformEmbedding f := by
  simp_rw [isUniformEmbedding_iff, hg.isUniformInducing.of_comp_iff, hg.injective.of_comp_iff f]

@[deprecated (since := "2024-10-01")]
alias UniformEmbedding.of_comp_iff := IsUniformEmbedding.of_comp_iff

theorem Equiv.isUniformEmbedding {Œ± Œ≤ : Type*} [UniformSpace Œ±] [UniformSpace Œ≤] (f : Œ± ‚âÉ Œ≤)
    (h‚ÇÅ : UniformContinuous f) (h‚ÇÇ : UniformContinuous f.symm) : IsUniformEmbedding f :=
  isUniformEmbedding_iff'.2 ‚ü®f.injective, h‚ÇÅ, by rwa [‚Üê Equiv.prodCongr_apply, ‚Üê map_equiv_symm]‚ü©

@[deprecated (since := "2024-10-01")]
alias Equiv.uniformEmbedding := Equiv.isUniformEmbedding

theorem isUniformEmbedding_inl : IsUniformEmbedding (Sum.inl : Œ± ‚Üí Œ± ‚äï Œ≤) :=
  isUniformEmbedding_iff'.2 ‚ü®Sum.inl_injective, uniformContinuous_inl, fun s hs =>
    ‚ü®Prod.map Sum.inl Sum.inl '' s ‚à™ range (Prod.map Sum.inr Sum.inr),
      union_mem_sup (image_mem_map hs) range_mem_map,
      fun x h => by simpa [Prod.map_apply'] using h‚ü©‚ü©

@[deprecated (since := "2024-10-01")]
alias uniformEmbedding_inl := isUniformEmbedding_inl

theorem isUniformEmbedding_inr : IsUniformEmbedding (Sum.inr : Œ≤ ‚Üí Œ± ‚äï Œ≤) :=
  isUniformEmbedding_iff'.2 ‚ü®Sum.inr_injective, uniformContinuous_inr, fun s hs =>
    ‚ü®range (Prod.map Sum.inl Sum.inl) ‚à™ Prod.map Sum.inr Sum.inr '' s,
      union_mem_sup range_mem_map (image_mem_map hs),
      fun x h => by simpa [Prod.map_apply'] using h‚ü©‚ü©

@[deprecated (since := "2024-10-01")]
alias uniformEmbedding_inr := isUniformEmbedding_inr

/-- If the domain of a `IsUniformInducing` map `f` is a T‚ÇÄ space, then `f` is injective,
hence it is a `IsUniformEmbedding`. -/
protected theorem IsUniformInducing.isUniformEmbedding [T0Space Œ±] {f : Œ± ‚Üí Œ≤}
    (hf : IsUniformInducing f) : IsUniformEmbedding f :=
  ‚ü®hf, hf.isInducing.injective‚ü©

@[deprecated (since := "2024-10-05")]
alias UniformInducing.isUniformEmbedding := IsUniformInducing.isUniformEmbedding

@[deprecated (since := "2024-10-01")]
alias IsUniformInducing.uniformEmbedding := IsUniformInducing.isUniformEmbedding

theorem isUniformEmbedding_iff_isUniformInducing [T0Space Œ±] {f : Œ± ‚Üí Œ≤} :
    IsUniformEmbedding f ‚Üî IsUniformInducing f :=
  ‚ü®IsUniformEmbedding.isUniformInducing, IsUniformInducing.isUniformEmbedding‚ü©

@[deprecated (since := "2024-10-05")]
alias isUniformEmbedding_iff_uniformInducing := isUniformEmbedding_iff_isUniformInducing

@[deprecated (since := "2024-10-01")]
alias uniformEmbedding_iff_isUniformInducing := isUniformEmbedding_iff_isUniformInducing

/-- If a map `f : Œ± ‚Üí Œ≤` sends any two distinct points to point that are **not** related by a fixed
`s ‚àà ùì§ Œ≤`, then `f` is uniform inducing with respect to the discrete uniformity on `Œ±`:
the preimage of `ùì§ Œ≤` under `Prod.map f f` is the principal filter generated by the diagonal in
`Œ± √ó Œ±`. -/
theorem comap_uniformity_of_spaced_out {Œ±} {f : Œ± ‚Üí Œ≤} {s : Set (Œ≤ √ó Œ≤)} (hs : s ‚àà ùì§ Œ≤)
    (hf : Pairwise fun x y => (f x, f y) ‚àâ s) : comap (Prod.map f f) (ùì§ Œ≤) = ùìü idRel := by
  refine le_antisymm ?_ (@refl_le_uniformity Œ± (UniformSpace.comap f _))
  calc
    comap (Prod.map f f) (ùì§ Œ≤) ‚â§ comap (Prod.map f f) (ùìü s) := comap_mono (le_principal_iff.2 hs)
    _ = ùìü (Prod.map f f ‚Åª¬π' s) := comap_principal
    _ ‚â§ ùìü idRel := principal_mono.2 ?_
  rintro ‚ü®x, y‚ü©; simpa [not_imp_not] using @hf x y

/-- If a map `f : Œ± ‚Üí Œ≤` sends any two distinct points to point that are **not** related by a fixed
`s ‚àà ùì§ Œ≤`, then `f` is a uniform embedding with respect to the discrete uniformity on `Œ±`. -/
theorem isUniformEmbedding_of_spaced_out {Œ±} {f : Œ± ‚Üí Œ≤} {s : Set (Œ≤ √ó Œ≤)} (hs : s ‚àà ùì§ Œ≤)
    (hf : Pairwise fun x y => (f x, f y) ‚àâ s) : @IsUniformEmbedding Œ± Œ≤ ‚ä• ‚Äπ_‚Ä∫ f := by
  let _ : UniformSpace Œ± := ‚ä•; have := discreteTopology_bot Œ±
  exact IsUniformInducing.isUniformEmbedding ‚ü®comap_uniformity_of_spaced_out hs hf‚ü©

@[deprecated (since := "2024-10-01")]
alias uniformEmbedding_of_spaced_out := isUniformEmbedding_of_spaced_out

protected lemma IsUniformEmbedding.isEmbedding {f : Œ± ‚Üí Œ≤} (h : IsUniformEmbedding f) :
    IsEmbedding f where
  toIsInducing := h.toIsUniformInducing.isInducing
  injective := h.injective

@[deprecated (since := "2024-10-26")]
alias IsUniformEmbedding.embedding := IsUniformEmbedding.isEmbedding

@[deprecated (since := "2024-10-01")]
alias UniformEmbedding.embedding := IsUniformEmbedding.isEmbedding

theorem IsUniformEmbedding.isDenseEmbedding {f : Œ± ‚Üí Œ≤} (h : IsUniformEmbedding f)
    (hd : DenseRange f) : IsDenseEmbedding f :=
  { h.isEmbedding with dense := hd }

@[deprecated (since := "2024-10-01")]
alias UniformEmbedding.isDenseEmbedding := IsUniformEmbedding.isDenseEmbedding

theorem isClosedEmbedding_of_spaced_out {Œ±} [TopologicalSpace Œ±] [DiscreteTopology Œ±]
    [T0Space Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set (Œ≤ √ó Œ≤)} (hs : s ‚àà ùì§ Œ≤)
    (hf : Pairwise fun x y => (f x, f y) ‚àâ s) : IsClosedEmbedding f := by
  rcases @DiscreteTopology.eq_bot Œ± _ _ with rfl; let _ : UniformSpace Œ± := ‚ä•
  exact
    { (isUniformEmbedding_of_spaced_out hs hf).isEmbedding with
      isClosed_range := isClosed_range_of_spaced_out hs hf }

@[deprecated (since := "2024-10-20")]
alias closedEmbedding_of_spaced_out := isClosedEmbedding_of_spaced_out

theorem closure_image_mem_nhds_of_isUniformInducing {s : Set (Œ± √ó Œ±)} {e : Œ± ‚Üí Œ≤} (b : Œ≤)
    (he‚ÇÅ : IsUniformInducing e) (he‚ÇÇ : IsDenseInducing e) (hs : s ‚àà ùì§ Œ±) :
    ‚àÉ a, closure (e '' { a' | (a, a') ‚àà s }) ‚àà ùìù b := by
  obtain ‚ü®U, ‚ü®hU, hUo, hsymm‚ü©, hs‚ü© :
    ‚àÉ U, (U ‚àà ùì§ Œ≤ ‚àß IsOpen U ‚àß IsSymmetricRel U) ‚àß Prod.map e e ‚Åª¬π' U ‚äÜ s := by
      rwa [‚Üê he‚ÇÅ.comap_uniformity, (uniformity_hasBasis_open_symmetric.comap _).mem_iff] at hs
  rcases he‚ÇÇ.dense.mem_nhds (UniformSpace.ball_mem_nhds b hU) with ‚ü®a, ha‚ü©
  refine ‚ü®a, mem_of_superset ?_ (closure_mono <| image_subset _ <| UniformSpace.ball_mono hs a)‚ü©
  have ho : IsOpen (UniformSpace.ball (e a) U) := UniformSpace.isOpen_ball (e a) hUo
  refine mem_of_superset (ho.mem_nhds <| (UniformSpace.mem_ball_symmetry hsymm).2 ha) fun y hy => ?_
  refine mem_closure_iff_nhds.2 fun V hV => ?_
  rcases he‚ÇÇ.dense.mem_nhds (inter_mem hV (ho.mem_nhds hy)) with ‚ü®x, hxV, hxU‚ü©
  exact ‚ü®e x, hxV, mem_image_of_mem e hxU‚ü©

@[deprecated (since := "2024-10-05")]
alias closure_image_mem_nhds_of_uniformInducing := closure_image_mem_nhds_of_isUniformInducing

theorem isUniformEmbedding_subtypeEmb (p : Œ± ‚Üí Prop) {e : Œ± ‚Üí Œ≤} (ue : IsUniformEmbedding e)
    (de : IsDenseEmbedding e) : IsUniformEmbedding (IsDenseEmbedding.subtypeEmb p e) :=
  { comap_uniformity := by
      simp [comap_comap, Function.comp_def, IsDenseEmbedding.subtypeEmb, uniformity_subtype,
        ue.comap_uniformity.symm]
    injective := (de.subtype p).injective }

@[deprecated (since := "2024-10-01")]
alias uniformEmbedding_subtypeEmb := isUniformEmbedding_subtypeEmb

theorem IsUniformEmbedding.prod {Œ±' : Type*} {Œ≤' : Type*} [UniformSpace Œ±'] [UniformSpace Œ≤']
    {e‚ÇÅ : Œ± ‚Üí Œ±'} {e‚ÇÇ : Œ≤ ‚Üí Œ≤'} (h‚ÇÅ : IsUniformEmbedding e‚ÇÅ) (h‚ÇÇ : IsUniformEmbedding e‚ÇÇ) :
    IsUniformEmbedding fun p : Œ± √ó Œ≤ => (e‚ÇÅ p.1, e‚ÇÇ p.2) where
  toIsUniformInducing := h‚ÇÅ.isUniformInducing.prod h‚ÇÇ.isUniformInducing
  injective := h‚ÇÅ.injective.prodMap h‚ÇÇ.injective

@[deprecated (since := "2024-10-01")]
alias UniformEmbedding.prod := IsUniformEmbedding.prod

/-- A set is complete iff its image under a uniform inducing map is complete. -/
theorem isComplete_image_iff {m : Œ± ‚Üí Œ≤} {s : Set Œ±} (hm : IsUniformInducing m) :
    IsComplete (m '' s) ‚Üî IsComplete s := by
  have fact1 : SurjOn (map m) (Iic <| ùìü s) (Iic <| ùìü <| m '' s) := surjOn_image .. |>.filter_map_Iic
  have fact2 : MapsTo (map m) (Iic <| ùìü s) (Iic <| ùìü <| m '' s) := mapsTo_image .. |>.filter_map_Iic
  simp_rw [IsComplete, imp.swap (a := Cauchy _), ‚Üê mem_Iic (b := ùìü _), fact1.forall fact2,
    hm.cauchy_map_iff, exists_mem_image, map_le_iff_le_comap, hm.isInducing.nhds_eq_comap]

/-- If `f : X ‚Üí Y` is an `IsUniformInducing` map, the image `f '' s` of a set `s` is complete
  if and only if `s` is complete. -/
theorem IsUniformInducing.isComplete_iff {f : Œ± ‚Üí Œ≤} {s : Set Œ±} (hf : IsUniformInducing f) :
    IsComplete (f '' s) ‚Üî IsComplete s := isComplete_image_iff hf

@[deprecated (since := "2024-10-05")]
alias UniformInducing.isComplete_iff := IsUniformInducing.isComplete_iff

/-- If `f : X ‚Üí Y` is an `IsUniformEmbedding`, the image `f '' s` of a set `s` is complete
  if and only if `s` is complete. -/
theorem IsUniformEmbedding.isComplete_iff {f : Œ± ‚Üí Œ≤} {s : Set Œ±} (hf : IsUniformEmbedding f) :
    IsComplete (f '' s) ‚Üî IsComplete s := hf.isUniformInducing.isComplete_iff

@[deprecated (since := "2024-10-01")]
alias UniformEmbedding.isComplete_iff := IsUniformEmbedding.isComplete_iff

/-- Sets of a subtype are complete iff their image under the coercion is complete. -/
theorem Subtype.isComplete_iff {p : Œ± ‚Üí Prop} {s : Set { x // p x }} :
    IsComplete s ‚Üî IsComplete ((‚Üë) '' s : Set Œ±) :=
  isUniformEmbedding_subtype_val.isComplete_iff.symm

alias ‚ü®isComplete_of_complete_image, _‚ü© := isComplete_image_iff

theorem completeSpace_iff_isComplete_range {f : Œ± ‚Üí Œ≤} (hf : IsUniformInducing f) :
    CompleteSpace Œ± ‚Üî IsComplete (range f) := by
  rw [completeSpace_iff_isComplete_univ, ‚Üê isComplete_image_iff hf, image_univ]

alias ‚ü®_, IsUniformInducing.completeSpace‚ü© := completeSpace_iff_isComplete_range

@[deprecated (since := "2024-10-08")] alias UniformInducing.completeSpace :=
  IsUniformInducing.completeSpace

lemma IsUniformInducing.isComplete_range [CompleteSpace Œ±] (hf : IsUniformInducing f) :
    IsComplete (range f) :=
  (completeSpace_iff_isComplete_range hf).1 ‚Äπ_‚Ä∫

@[deprecated (since := "2024-10-05")]
alias UniformInducing.isComplete_range := IsUniformInducing.isComplete_range

/-- If `f` is a surjective uniform inducing map,
then its domain is a complete space iff its codomain is a complete space.
See also `_root_.completeSpace_congr` for a version that assumes `f` to be an equivalence. -/
theorem IsUniformInducing.completeSpace_congr {f : Œ± ‚Üí Œ≤} (hf : IsUniformInducing f)
    (hsurj : f.Surjective) : CompleteSpace Œ± ‚Üî CompleteSpace Œ≤ := by
  rw [completeSpace_iff_isComplete_range hf, hsurj.range_eq, completeSpace_iff_isComplete_univ]

@[deprecated (since := "2024-10-05")]
alias UniformInducing.completeSpace_congr := IsUniformInducing.completeSpace_congr

theorem SeparationQuotient.completeSpace_iff :
    CompleteSpace (SeparationQuotient Œ±) ‚Üî CompleteSpace Œ± :=
  .symm <| isUniformInducing_mk.completeSpace_congr surjective_mk

instance SeparationQuotient.instCompleteSpace [CompleteSpace Œ±] :
    CompleteSpace (SeparationQuotient Œ±) :=
  completeSpace_iff.2 ‚Äπ_‚Ä∫

/-- See also `IsUniformInducing.completeSpace_congr`
for a version that works for non-injective maps. -/
theorem completeSpace_congr {e : Œ± ‚âÉ Œ≤} (he : IsUniformEmbedding e) :
    CompleteSpace Œ± ‚Üî CompleteSpace Œ≤ :=
  he.completeSpace_congr e.surjective

theorem completeSpace_coe_iff_isComplete {s : Set Œ±} : CompleteSpace s ‚Üî IsComplete s := by
  rw [completeSpace_iff_isComplete_range isUniformEmbedding_subtype_val.isUniformInducing,
    Subtype.range_coe]

alias ‚ü®_, IsComplete.completeSpace_coe‚ü© := completeSpace_coe_iff_isComplete

theorem IsClosed.completeSpace_coe [CompleteSpace Œ±] {s : Set Œ±} (hs : IsClosed s) :
    CompleteSpace s :=
  hs.isComplete.completeSpace_coe

theorem completeSpace_ulift_iff : CompleteSpace (ULift Œ±) ‚Üî CompleteSpace Œ± :=
  IsUniformInducing.completeSpace_congr ‚ü®rfl‚ü© ULift.down_surjective

/-- The lift of a complete space to another universe is still complete. -/
instance ULift.instCompleteSpace [CompleteSpace Œ±] : CompleteSpace (ULift Œ±) :=
  completeSpace_ulift_iff.2 ‚Äπ_‚Ä∫

theorem completeSpace_extension {m : Œ≤ ‚Üí Œ±} (hm : IsUniformInducing m) (dense : DenseRange m)
    (h : ‚àÄ f : Filter Œ≤, Cauchy f ‚Üí ‚àÉ x : Œ±, map m f ‚â§ ùìù x) : CompleteSpace Œ± :=
  ‚ü®fun {f : Filter Œ±} (hf : Cauchy f) =>
    let p : Set (Œ± √ó Œ±) ‚Üí Set Œ± ‚Üí Set Œ± := fun s t => { y : Œ± | ‚àÉ x : Œ±, x ‚àà t ‚àß (x, y) ‚àà s }
    let g := (ùì§ Œ±).lift fun s => f.lift' (p s)
    have mp‚ÇÄ : Monotone p := fun _ _ h _ _ ‚ü®x, xs, xa‚ü© => ‚ü®x, xs, h xa‚ü©
    have mp‚ÇÅ : ‚àÄ {s}, Monotone (p s) := fun h _ ‚ü®y, ya, yxs‚ü© => ‚ü®y, h ya, yxs‚ü©
    have : f ‚â§ g := le_iInf‚ÇÇ fun _ hs => le_iInf‚ÇÇ fun _ ht =>
      le_principal_iff.mpr <| mem_of_superset ht fun x hx => ‚ü®x, hx, refl_mem_uniformity hs‚ü©
    have : NeBot g := hf.left.mono this
    have : NeBot (comap m g) :=
      comap_neBot fun _ ht =>
        let ‚ü®t', ht', ht_mem‚ü© := (mem_lift_sets <| monotone_lift' monotone_const mp‚ÇÄ).mp ht
        let ‚ü®_, ht'', ht'_sub‚ü© := (mem_lift'_sets mp‚ÇÅ).mp ht_mem
        let ‚ü®x, hx‚ü© := hf.left.nonempty_of_mem ht''
        have h‚ÇÄ : NeBot (ùìù[range m] x) := dense.nhdsWithin_neBot x
        have h‚ÇÅ : { y | (x, y) ‚àà t' } ‚àà ùìù[range m] x :=
          @mem_inf_of_left Œ± (ùìù x) (ùìü (range m)) _ <| mem_nhds_left x ht'
        have h‚ÇÇ : range m ‚àà ùìù[range m] x :=
          @mem_inf_of_right Œ± (ùìù x) (ùìü (range m)) _ <| Subset.refl _
        have : { y | (x, y) ‚àà t' } ‚à© range m ‚àà ùìù[range m] x := @inter_mem Œ± (ùìù[range m] x) _ _ h‚ÇÅ h‚ÇÇ
        let ‚ü®_, xyt', b, b_eq‚ü© := h‚ÇÄ.nonempty_of_mem this
        ‚ü®b, b_eq.symm ‚ñ∏ ht'_sub ‚ü®x, hx, xyt'‚ü©‚ü©
    have : Cauchy g :=
      ‚ü®‚ÄπNeBot g‚Ä∫, fun _ hs =>
        let ‚ü®s‚ÇÅ, hs‚ÇÅ, comp_s‚ÇÅ‚ü© := comp_mem_uniformity_sets hs
        let ‚ü®s‚ÇÇ, hs‚ÇÇ, comp_s‚ÇÇ‚ü© := comp_mem_uniformity_sets hs‚ÇÅ
        let ‚ü®t, ht, (prod_t : t √óÀ¢ t ‚äÜ s‚ÇÇ)‚ü© := mem_prod_same_iff.mp (hf.right hs‚ÇÇ)
        have hg‚ÇÅ : p (preimage Prod.swap s‚ÇÅ) t ‚àà g :=
          mem_lift (symm_le_uniformity hs‚ÇÅ) <| @mem_lift' Œ± Œ± f _ t ht
        have hg‚ÇÇ : p s‚ÇÇ t ‚àà g := mem_lift hs‚ÇÇ <| @mem_lift' Œ± Œ± f _ t ht
        have hg : p (Prod.swap ‚Åª¬π' s‚ÇÅ) t √óÀ¢ p s‚ÇÇ t ‚àà g √óÀ¢ g := @prod_mem_prod Œ± Œ± _ _ g g hg‚ÇÅ hg‚ÇÇ
        (g √óÀ¢ g).sets_of_superset hg fun ‚ü®_, _‚ü© ‚ü®‚ü®c‚ÇÅ, c‚ÇÅt, hc‚ÇÅ‚ü©, ‚ü®c‚ÇÇ, c‚ÇÇt, hc‚ÇÇ‚ü©‚ü© =>
          have : (c‚ÇÅ, c‚ÇÇ) ‚àà t √óÀ¢ t := ‚ü®c‚ÇÅt, c‚ÇÇt‚ü©
          comp_s‚ÇÅ <| prodMk_mem_compRel hc‚ÇÅ <| comp_s‚ÇÇ <| prodMk_mem_compRel (prod_t this) hc‚ÇÇ‚ü©
    have : Cauchy (Filter.comap m g) := ‚ÄπCauchy g‚Ä∫.comap' (le_of_eq hm.comap_uniformity) ‚Äπ_‚Ä∫
    let ‚ü®x, (hx : map m (Filter.comap m g) ‚â§ ùìù x)‚ü© := h _ this
    have : ClusterPt x (map m (Filter.comap m g)) :=
      (le_nhds_iff_adhp_of_cauchy (this.map hm.uniformContinuous)).mp hx
    have : ClusterPt x g := this.mono map_comap_le
    ‚ü®x,
      calc
        f ‚â§ g := by assumption
        _ ‚â§ ùìù x := le_nhds_of_cauchy_adhp ‚ÄπCauchy g‚Ä∫ this
        ‚ü©‚ü©

lemma totallyBounded_image_iff {f : Œ± ‚Üí Œ≤} {s : Set Œ±} (hf : IsUniformInducing f) :
    TotallyBounded (f '' s) ‚Üî TotallyBounded s := by
  refine ‚ü®fun hs ‚Ü¶ ?_, fun h ‚Ü¶ h.image hf.uniformContinuous‚ü©
  simp_rw [(hf.basis_uniformity (basis_sets _)).totallyBounded_iff]
  intro t ht
  rcases exists_subset_image_finite_and.1 (hs.exists_subset ht) with ‚ü®u, -, hfin, h‚ü©
  use u, hfin
  rwa [biUnion_image, image_subset_iff, preimage_iUnion‚ÇÇ] at h

theorem totallyBounded_preimage {f : Œ± ‚Üí Œ≤} {s : Set Œ≤} (hf : IsUniformInducing f)
    (hs : TotallyBounded s) : TotallyBounded (f ‚Åª¬π' s) :=
  (totallyBounded_image_iff hf).1 <| hs.subset <| image_preimage_subset ..

instance CompleteSpace.sum [CompleteSpace Œ±] [CompleteSpace Œ≤] : CompleteSpace (Œ± ‚äï Œ≤) := by
  rw [completeSpace_iff_isComplete_univ, ‚Üê range_inl_union_range_inr]
  exact isUniformEmbedding_inl.isUniformInducing.isComplete_range.union
    isUniformEmbedding_inr.isUniformInducing.isComplete_range

end

theorem isUniformEmbedding_comap {Œ± : Type*} {Œ≤ : Type*} {f : Œ± ‚Üí Œ≤} [u : UniformSpace Œ≤]
    (hf : Function.Injective f) : @IsUniformEmbedding Œ± Œ≤ (UniformSpace.comap f u) u f :=
  @IsUniformEmbedding.mk _ _ (UniformSpace.comap f u) _ _
    (@IsUniformInducing.mk _ _ (UniformSpace.comap f u) _ _ rfl) hf

@[deprecated (since := "2024-10-01")]
alias uniformEmbedding_comap := isUniformEmbedding_comap

/-- Pull back a uniform space structure by an embedding, adjusting the new uniform structure to
make sure that its topology is defeq to the original one. -/
def Topology.IsEmbedding.comapUniformSpace {Œ± Œ≤} [TopologicalSpace Œ±] [u : UniformSpace Œ≤]
    (f : Œ± ‚Üí Œ≤) (h : IsEmbedding f) : UniformSpace Œ± :=
  (u.comap f).replaceTopology h.eq_induced

@[deprecated (since := "2024-10-26")]
alias Embedding.comapUniformSpace := IsEmbedding.comapUniformSpace

theorem Embedding.to_isUniformEmbedding {Œ± Œ≤} [TopologicalSpace Œ±] [u : UniformSpace Œ≤] (f : Œ± ‚Üí Œ≤)
    (h : IsEmbedding f) : @IsUniformEmbedding Œ± Œ≤ (h.comapUniformSpace f) u f :=
  let _ := h.comapUniformSpace f
  { comap_uniformity := rfl
    injective := h.injective }

@[deprecated (since := "2024-10-01")]
alias Embedding.to_uniformEmbedding := Embedding.to_isUniformEmbedding

section UniformExtension

variable {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*} [UniformSpace Œ±] [UniformSpace Œ≤] [UniformSpace Œ≥]
  {e : Œ≤ ‚Üí Œ±} (h_e : IsUniformInducing e) (h_dense : DenseRange e) {f : Œ≤ ‚Üí Œ≥}
  (h_f : UniformContinuous f)

local notation "œà" => IsDenseInducing.extend (IsUniformInducing.isDenseInducing h_e h_dense) f

include h_e h_dense h_f in
theorem uniformly_extend_exists [CompleteSpace Œ≥] (a : Œ±) : ‚àÉ c, Tendsto f (comap e (ùìù a)) (ùìù c) :=
  let de := h_e.isDenseInducing h_dense
  have : Cauchy (ùìù a) := cauchy_nhds
  have : Cauchy (comap e (ùìù a)) :=
    this.comap' (le_of_eq h_e.comap_uniformity) (de.comap_nhds_neBot _)
  have : Cauchy (map f (comap e (ùìù a))) := this.map h_f
  CompleteSpace.complete this

theorem uniform_extend_subtype [CompleteSpace Œ≥] {p : Œ± ‚Üí Prop} {e : Œ± ‚Üí Œ≤} {f : Œ± ‚Üí Œ≥} {b : Œ≤}
    {s : Set Œ±} (hf : UniformContinuous fun x : Subtype p => f x.val) (he : IsUniformEmbedding e)
    (hd : ‚àÄ x : Œ≤, x ‚àà closure (range e)) (hb : closure (e '' s) ‚àà ùìù b) (hs : IsClosed s)
    (hp : ‚àÄ x ‚àà s, p x) : ‚àÉ c, Tendsto f (comap e (ùìù b)) (ùìù c) := by
  have de : IsDenseEmbedding e := he.isDenseEmbedding hd
  have de' : IsDenseEmbedding (IsDenseEmbedding.subtypeEmb p e) := de.subtype p
  have ue' : IsUniformEmbedding (IsDenseEmbedding.subtypeEmb p e) :=
    isUniformEmbedding_subtypeEmb _ he de
  have : b ‚àà closure (e '' { x | p x }) :=
    (closure_mono <| monotone_image <| hp) (mem_of_mem_nhds hb)
  let ‚ü®c, hc‚ü© := uniformly_extend_exists ue'.isUniformInducing de'.dense hf ‚ü®b, this‚ü©
  replace hc : Tendsto (f ‚àò Subtype.val (p := p)) (((ùìù b).comap e).comap Subtype.val) (ùìù c) := by
    simpa only [nhds_subtype_eq_comap, comap_comap, IsDenseEmbedding.subtypeEmb_coe] using hc
  refine ‚ü®c, (tendsto_comap'_iff ?_).1 hc‚ü©
  rw [Subtype.range_coe_subtype]
  exact ‚ü®_, hb, by rwa [‚Üê de.isInducing.closure_eq_preimage_closure_image, hs.closure_eq]‚ü©

include h_e h_f in
theorem uniformly_extend_spec [CompleteSpace Œ≥] (a : Œ±) : Tendsto f (comap e (ùìù a)) (ùìù (œà a)) := by
  simpa only [IsDenseInducing.extend] using
    tendsto_nhds_limUnder (uniformly_extend_exists h_e ‚Äπ_‚Ä∫ h_f _)

include h_f in
theorem uniformContinuous_uniformly_extend [CompleteSpace Œ≥] : UniformContinuous œà := fun d hd =>
  let ‚ü®s, hs, hs_comp‚ü© := comp3_mem_uniformity hd
  have h_pnt : ‚àÄ {a m}, m ‚àà ùìù a ‚Üí ‚àÉ c ‚àà f '' (e ‚Åª¬π' m), (c, œà a) ‚àà s ‚àß (œà a, c) ‚àà s :=
    fun {a m} hm =>
    have nb : NeBot (map f (comap e (ùìù a))) :=
      ((h_e.isDenseInducing h_dense).comap_nhds_neBot _).map _
    have :
      f '' (e ‚Åª¬π' m) ‚à© ({ c | (c, œà a) ‚àà s } ‚à© { c | (œà a, c) ‚àà s }) ‚àà map f (comap e (ùìù a)) :=
      inter_mem (image_mem_map <| preimage_mem_comap <| hm)
        (uniformly_extend_spec h_e h_dense h_f _
          (inter_mem (mem_nhds_right _ hs) (mem_nhds_left _ hs)))
    nb.nonempty_of_mem this
  have : (Prod.map f f) ‚Åª¬π' s ‚àà ùì§ Œ≤ := h_f hs
  have : (Prod.map f f) ‚Åª¬π' s ‚àà comap (Prod.map e e) (ùì§ Œ±) := by
    rwa [‚Üê h_e.comap_uniformity] at this
  let ‚ü®t, ht, ts‚ü© := this
  show (Prod.map œà œà) ‚Åª¬π' d ‚àà ùì§ Œ± from
    mem_of_superset (interior_mem_uniformity ht) fun ‚ü®x‚ÇÅ, x‚ÇÇ‚ü© hx_t => by
      have : interior t ‚àà ùìù (x‚ÇÅ, x‚ÇÇ) := isOpen_interior.mem_nhds hx_t
      let ‚ü®m‚ÇÅ, hm‚ÇÅ, m‚ÇÇ, hm‚ÇÇ, (hm : m‚ÇÅ √óÀ¢ m‚ÇÇ ‚äÜ interior t)‚ü© := mem_nhds_prod_iff.mp this
      obtain ‚ü®_, ‚ü®a, ha‚ÇÅ, rfl‚ü©, _, ha‚ÇÇ‚ü© := h_pnt hm‚ÇÅ
      obtain ‚ü®_, ‚ü®b, hb‚ÇÅ, rfl‚ü©, hb‚ÇÇ, _‚ü© := h_pnt hm‚ÇÇ
      have : Prod.map f f (a, b) ‚àà s :=
        ts <| mem_preimage.2 <| interior_subset (@hm (e a, e b) ‚ü®ha‚ÇÅ, hb‚ÇÅ‚ü©)
      exact hs_comp ‚ü®f a, ha‚ÇÇ, ‚ü®f b, this, hb‚ÇÇ‚ü©‚ü©

variable [T0Space Œ≥]

include h_f in
theorem uniformly_extend_of_ind (b : Œ≤) : œà (e b) = f b :=
  IsDenseInducing.extend_eq_at _ h_f.continuous.continuousAt

theorem uniformly_extend_unique {g : Œ± ‚Üí Œ≥} (hg : ‚àÄ b, g (e b) = f b) (hc : Continuous g) : œà = g :=
  IsDenseInducing.extend_unique _ hg hc

end UniformExtension

section DenseExtension

variable {Œ± Œ≤ : Type*} [UniformSpace Œ±] [UniformSpace Œ≤]

theorem isUniformInducing_val (s : Set Œ±) :
    IsUniformInducing (@Subtype.val Œ± s) := ‚ü®uniformity_setCoe‚ü©

namespace Dense

variable {s : Set Œ±} {f : s ‚Üí Œ≤}

theorem extend_exists [CompleteSpace Œ≤] (hs : Dense s) (hf : UniformContinuous f) (a : Œ±) :
    ‚àÉ b, Tendsto f (comap (‚Üë) (ùìù a)) (ùìù b) :=
  uniformly_extend_exists (isUniformInducing_val s) hs.denseRange_val hf a

theorem extend_spec [CompleteSpace Œ≤] (hs : Dense s) (hf : UniformContinuous f) (a : Œ±) :
    Tendsto f (comap (‚Üë) (ùìù a)) (ùìù (hs.extend f a)) :=
  uniformly_extend_spec (isUniformInducing_val s) hs.denseRange_val hf a

theorem uniformContinuous_extend [CompleteSpace Œ≤] (hs : Dense s) (hf : UniformContinuous f) :
    UniformContinuous (hs.extend f) :=
  uniformContinuous_uniformly_extend (isUniformInducing_val s) hs.denseRange_val hf

variable [T0Space Œ≤]

theorem extend_of_ind (hs : Dense s) (hf : UniformContinuous f) (x : s) :
    hs.extend f x = f x :=
  IsDenseInducing.extend_eq_at _ hf.continuous.continuousAt

end Dense

end DenseExtension
