/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import Mathlib.Topology.UniformSpace.CompactConvergence
import Mathlib.Topology.UniformSpace.Equicontinuity
import Mathlib.Topology.UniformSpace.Equiv

/-!
# Ascoli Theorem

In this file, we prove the general **Arzela-Ascoli theorem**, and various related statements about
the topology of equicontinuous subsets of `X â†’áµ¤[ğ”–] Î±`, where `X` is a topological space, `ğ”–` is
a family of compact subsets of `X`, and `Î±` is a uniform space.

## Main statements

* If `X` is a compact space, then the uniform structures of uniform convergence and pointwise
  convergence coincide on equicontinuous subsets. This is the key fact that makes equicontinuity
  important in functional analysis. We state various versions of it:
  - as an equality of `UniformSpace`s: `Equicontinuous.comap_uniformFun_eq`
  - in terms of `IsUniformInducing`: `Equicontinuous.isUniformInducing_uniformFun_iff_pi`
  - in terms of `IsInducing`: `Equicontinuous.inducing_uniformFun_iff_pi`
  - in terms of convergence along a filter: `Equicontinuous.tendsto_uniformFun_iff_pi`
* As a consequence, if `ğ”–` is a family of compact subsets of `X`, then the uniform structures of
  uniform convergence on `ğ”–` and pointwise convergence on `â‹ƒâ‚€ ğ”–` coincide on equicontinuous
  subsets. Again, we prove multiple variations:
  - as an equality of `UniformSpace`s: `EquicontinuousOn.comap_uniformOnFun_eq`
  - in terms of `IsUniformInducing`: `EquicontinuousOn.isUniformInducing_uniformOnFun_iff_pi'`
  - in terms of `IsInducing`: `EquicontinuousOn.inducing_uniformOnFun_iff_pi'`
  - in terms of convergence along a filter: `EquicontinuousOn.tendsto_uniformOnFun_iff_pi'`
* The **Arzela-Ascoli theorem** follows from the previous fact and Tykhonov's theorem.
  All of its variations can be found under the `ArzelaAscoli` namespace.

## Implementation details

* The statements in this file may be a bit daunting because we prove everything for families and
  embeddings instead of subspaces with the subspace topology. This is done because, in practice,
  one would rarely work with `X â†’áµ¤[ğ”–] Î±` directly, so we need to provide API for bringing back the
  statements to various other types, such as `C(X, Y)` or `E â†’L[ğ•œ] F`. To counteract this, all
  statements (as well as most proofs!) are documented quite thoroughly.

* A lot of statements assume `âˆ€ K âˆˆ ğ”–, EquicontinuousOn F K` instead of the more natural
  `EquicontinuousOn F (â‹ƒâ‚€ ğ”–)`. This is in order to keep the most generality, as the first statement
  is strictly weaker.

* In Bourbaki, the usual Arzela-Ascoli compactness theorem follows from a similar total boundedness
  result. Here we go directly for the compactness result, which is the most useful in practice, but
  this will be an easy addition/refactor if we ever need it.

## TODO

* Prove that, on an equicontinuous family, pointwise convergence and pointwise convergence on a
  dense subset coincide, and deduce metrizability criteria for equicontinuous subsets.

* Prove the total boundedness version of the theorem

* Prove the converse statement: if a subset of `X â†’áµ¤[ğ”–] Î±` is compact, then it is equicontinuous
  on each `K âˆˆ ğ”–`.

## References

* [N. Bourbaki, *General Topology, Chapter X*][bourbaki1966]

## Tags

equicontinuity, uniform convergence, ascoli
-/

open Set Filter Uniformity Topology Function UniformConvergence

variable {Î¹ X Î± : Type*} [TopologicalSpace X] [UniformSpace Î±] {F : Î¹ â†’ X â†’ Î±}

/-- Let `X` be a compact topological space, `Î±` a uniform space, and `F : Î¹ â†’ (X â†’ Î±)` an
equicontinuous family. Then, the uniform structures of uniform convergence and pointwise
convergence induce the same uniform structure on `Î¹`.

In other words, pointwise convergence and uniform convergence coincide on an equicontinuous
subset of `X â†’ Î±`.

Consider using `Equicontinuous.isUniformInducing_uniformFun_iff_pi` and
`Equicontinuous.inducing_uniformFun_iff_pi` instead, to avoid rewriting instances. -/
theorem Equicontinuous.comap_uniformFun_eq [CompactSpace X] (F_eqcont : Equicontinuous F) :
    (UniformFun.uniformSpace X Î±).comap F =
    (Pi.uniformSpace _).comap F := by
  -- The `â‰¤` inequality is trivial
  refine le_antisymm (UniformSpace.comap_mono UniformFun.uniformContinuous_toFun) ?_
  -- A bit of rewriting to get a nice intermediate statement.
  simp_rw [UniformSpace.comap, UniformSpace.le_def, uniformity_comap, Pi.uniformity,
    Filter.comap_iInf, comap_comap, Function.comp_def]
  refine ((UniformFun.hasBasis_uniformity X Î±).comap (Prod.map F F)).ge_iff.mpr ?_
  -- Core of the proof: we need to show that, for any entourage `U` in `Î±`,
  -- the set `ğ“(U) := {(i,j) : Î¹ Ã— Î¹ | âˆ€ x : X, (F i x, F j x) âˆˆ U}` belongs to the filter
  -- `â¨… x, comap ((i,j) â†¦ (F i x, F j x)) (ğ“¤ Î±)`.
  -- In other words, we have to show that it contains a finite intersection of
  -- sets of the form `ğ’(V, x) := {(i,j) : Î¹ Ã— Î¹ | (F i x, F j x) âˆˆ V}` for some
  -- `x : X` and `V âˆˆ ğ“¤ Î±`.
  intro U hU
  -- We will do an `Îµ/3` argument, so we start by choosing a symmetric entourage `V âˆˆ ğ“¤ Î±`
  -- such that `V â—‹ V â—‹ V âŠ† U`.
  rcases comp_comp_symm_mem_uniformity_sets hU with âŸ¨V, hV, Vsymm, hVUâŸ©
  -- Set `Î© x := {y | âˆ€ i, (F i x, F i y) âˆˆ V}`. The equicontinuity of `F` guarantees that
  -- each `Î© x` is a neighborhood of `x`.
  let Î© x : Set X := {y | âˆ€ i, (F i x, F i y) âˆˆ V}
  -- Hence, by compactness of `X`, we can find some `A âŠ† X` finite such that the `Î© a`s for `a âˆˆ A`
  -- still cover `X`.
  rcases CompactSpace.elim_nhds_subcover Î© (fun x â†¦ F_eqcont x V hV) with âŸ¨A, AcoverâŸ©
  -- We now claim that `â‹‚ a âˆˆ A, ğ’(V, a) âŠ† ğ“(U)`.
  have : (â‹‚ a âˆˆ A, {ij : Î¹ Ã— Î¹ | (F ij.1 a, F ij.2 a) âˆˆ V}) âŠ†
      (Prod.map F F) â»Â¹' UniformFun.gen X Î± U := by
    -- Given `(i, j) âˆˆ â‹‚ a âˆˆ A, ğ’(V, a)` and `x : X`, we have to prove that `(F i x, F j x) âˆˆ U`.
    rintro âŸ¨i, jâŸ© hij x
    rw [mem_iInterâ‚‚] at hij
    -- We know that `x âˆˆ Î© a` for some `a âˆˆ A`, so that both `(F i x, F i a)` and `(F j a, F j x)`
    -- are in `V`.
    rcases mem_iUnionâ‚‚.mp (Acover.symm.subset <| mem_univ x) with âŸ¨a, ha, haxâŸ©
    -- Since `(i, j) âˆˆ ğ’(V, a)` we also have `(F i a, F j a) âˆˆ V`, and finally we get
    -- `(F i x, F j x) âˆˆ V â—‹ V â—‹ V âŠ† U`.
    exact hVU <| SetRel.prodMk_mem_comp (SetRel.prodMk_mem_comp (SetRel.symm V <| hax i) (hij a ha))
      (hax j)
  -- This completes the proof.
  exact mem_of_superset
    (A.iInter_mem_sets.mpr fun x _ â†¦ mem_iInf_of_mem x <| preimage_mem_comap hV) this

/-- Let `X` be a compact topological space, `Î±` a uniform space, and `F : Î¹ â†’ (X â†’ Î±)` an
equicontinuous family. Then, the uniform structures of uniform convergence and pointwise
convergence induce the same uniform structure on `Î¹`.

In other words, pointwise convergence and uniform convergence coincide on an equicontinuous
subset of `X â†’ Î±`.

This is a version of `Equicontinuous.comap_uniformFun_eq` stated in terms of `IsUniformInducing`
for convenience. -/
lemma Equicontinuous.isUniformInducing_uniformFun_iff_pi [UniformSpace Î¹] [CompactSpace X]
    (F_eqcont : Equicontinuous F) :
    IsUniformInducing (UniformFun.ofFun âˆ˜ F) â†” IsUniformInducing F := by
  rw [isUniformInducing_iff_uniformSpace, isUniformInducing_iff_uniformSpace,
      â† F_eqcont.comap_uniformFun_eq]
  rfl

/-- Let `X` be a compact topological space, `Î±` a uniform space, and `F : Î¹ â†’ (X â†’ Î±)` an
equicontinuous family. Then, the topologies of uniform convergence and pointwise convergence induce
the same topology on `Î¹`.

In other words, pointwise convergence and uniform convergence coincide on an equicontinuous
subset of `X â†’ Î±`.

This is a consequence of `Equicontinuous.comap_uniformFun_eq`, stated in terms of `IsInducing`
for convenience. -/
lemma Equicontinuous.inducing_uniformFun_iff_pi [TopologicalSpace Î¹] [CompactSpace X]
    (F_eqcont : Equicontinuous F) :
    IsInducing (UniformFun.ofFun âˆ˜ F) â†” IsInducing F := by
  rw [isInducing_iff, isInducing_iff]
  change (_ = (UniformFun.uniformSpace X Î± |>.comap F |>.toTopologicalSpace)) â†”
         (_ = (Pi.uniformSpace _ |>.comap F |>.toTopologicalSpace))
  rw [F_eqcont.comap_uniformFun_eq]

/-- Let `X` be a compact topological space, `Î±` a uniform space, `F : Î¹ â†’ (X â†’ Î±)` an
equicontinuous family, and `â„±` a filter on `Î¹`. Then, `F` tends *uniformly* to `f : X â†’ Î±` along
`â„±` iff it tends to `f` *pointwise* along `â„±`. -/
theorem Equicontinuous.tendsto_uniformFun_iff_pi [CompactSpace X]
    (F_eqcont : Equicontinuous F) (â„± : Filter Î¹) (f : X â†’ Î±) :
    Tendsto (UniformFun.ofFun âˆ˜ F) â„± (ğ“ <| UniformFun.ofFun f) â†”
    Tendsto F â„± (ğ“ f) := by
  -- Assume `â„±` is non-trivial.
  rcases â„±.eq_or_neBot with rfl | â„±_ne
  Â· simp
  constructor <;> intro H
  -- The forward direction is always true, the interesting part is the converse.
  Â· exact UniformFun.uniformContinuous_toFun.continuous.tendsto _ |>.comp H
  -- To prove it, assume that `F` tends to `f` *pointwise* along `â„±`.
  Â· set S : Set (X â†’ Î±) := closure (range F)
    set ğ’¢ : Filter S := comap (â†‘) (map F â„±)
    -- We would like to use `Equicontinuous.comap_uniformFun_eq`, but applying it to `F` is not
    -- enough since `f` has no reason to be in the range of `F`.
    -- Instead, we will apply it to the inclusion `(â†‘) : S â†’ (X â†’ Î±)` where `S` is the closure of
    -- the range of `F` *for the product topology*.
    -- We know that `S` is still equicontinuous...
    have hS : S.Equicontinuous := closure' (by rwa [equicontinuous_iff_range] at F_eqcont)
      continuous_id
    -- ... hence, as announced, the product topology and uniform convergence topology
    -- coincide on `S`.
    have ind : IsInducing (UniformFun.ofFun âˆ˜ (â†‘) : S â†’ X â†’áµ¤ Î±) :=
      hS.inducing_uniformFun_iff_pi.mpr âŸ¨rflâŸ©
    -- By construction, `f` is in `S`.
    have f_mem : f âˆˆ S := mem_closure_of_tendsto H range_mem_map
    -- To conclude, we just have to translate our hypothesis and goal as statements about
    -- `S`, on which we know the two topologies at play coincide.
    -- For this, we define a filter on `S` by `ğ’¢ := comap (â†‘) (map F â„±)`, and note that
    -- it satisfies `map (â†‘) ğ’¢ = map F â„±`. Thus, both our hypothesis and our goal
    -- can be rewritten as `ğ’¢ â‰¤ ğ“ f`, where the neighborhood filter in the RHS corresponds
    -- to one of the two topologies at play on `S`. Since they coincide, we are done.
    have hğ’¢â„± : map (â†‘) ğ’¢ = map F â„± := Filter.map_comap_of_mem
      (Subtype.range_coe â–¸ mem_of_superset range_mem_map subset_closure)
    have H' : Tendsto id ğ’¢ (ğ“ âŸ¨f, f_memâŸ©) := by
      rwa [tendsto_id', nhds_induced, â† map_le_iff_le_comap, hğ’¢â„±]
    rwa [ind.tendsto_nhds_iff, comp_id, â† tendsto_map'_iff, hğ’¢â„±] at H'

/-- Let `X` be a topological space, `ğ”–` a family of compact subsets of `X`, `Î±` a uniform space,
and `F : Î¹ â†’ (X â†’ Î±)` a family which is equicontinuous on each `K âˆˆ ğ”–`. Then, the uniform
structures of uniform convergence on `ğ”–` and pointwise convergence on `â‹ƒâ‚€ ğ”–` induce the same
uniform structure on `Î¹`.

In particular, pointwise convergence and compact convergence coincide on an equicontinuous
subset of `X â†’ Î±`.

Consider using `EquicontinuousOn.isUniformInducing_uniformOnFun_iff_pi'` and
`EquicontinuousOn.inducing_uniformOnFun_iff_pi'` instead to avoid rewriting instances,
as well as their unprimed versions in case `ğ”–` covers `X`. -/
theorem EquicontinuousOn.comap_uniformOnFun_eq {ğ”– : Set (Set X)} (ğ”–_compact : âˆ€ K âˆˆ ğ”–, IsCompact K)
    (F_eqcont : âˆ€ K âˆˆ ğ”–, EquicontinuousOn F K) :
    (UniformOnFun.uniformSpace X Î± ğ”–).comap F =
    (Pi.uniformSpace _).comap ((â‹ƒâ‚€ ğ”–).restrict âˆ˜ F) := by
  -- Recall that the uniform structure on `X â†’áµ¤[ğ”–] Î±` is the one induced by all the maps
  -- `K.restrict : (X â†’áµ¤[ğ”–] Î±) â†’ (K â†’áµ¤ Î±)` for `K âˆˆ ğ”–`. Its pullback along `F`, which is
  -- the LHS of our goal, is thus the uniform structure induced by the maps
  -- `K.restrict âˆ˜ F : Î¹ â†’ (K â†’áµ¤ Î±)` for `K âˆˆ ğ”–`.
  have H1 : (UniformOnFun.uniformSpace X Î± ğ”–).comap F =
      â¨… (K âˆˆ ğ”–), (UniformFun.uniformSpace _ _).comap (K.restrict âˆ˜ F) := by
    simp_rw [UniformOnFun.uniformSpace, UniformSpace.comap_iInf, â† UniformSpace.comap_comap,
      UniformFun.ofFun, Equiv.coe_fn_mk, UniformOnFun.toFun, UniformOnFun.ofFun, Function.comp_def,
      UniformFun, Equiv.coe_fn_symm_mk]
  -- Now, note that a similar fact is true for the uniform structure on `X â†’ Î±` induced by
  -- the map `(â‹ƒâ‚€ ğ”–).restrict : (X â†’ Î±) â†’ ((â‹ƒâ‚€ ğ”–) â†’ Î±)`: it is equal to the one induced by
  -- all maps `K.restrict : (X â†’ Î±) â†’ (K â†’ Î±)` for `K âˆˆ ğ”–`, which means that the RHS of our
  -- goal is the uniform structure induced by the maps `K.restrict âˆ˜ F : Î¹ â†’ (K â†’ Î±)` for `K âˆˆ ğ”–`.
  have H2 : (Pi.uniformSpace _).comap ((â‹ƒâ‚€ ğ”–).restrict âˆ˜ F) =
      â¨… (K âˆˆ ğ”–), (Pi.uniformSpace _).comap (K.restrict âˆ˜ F) := by
    simp_rw [UniformSpace.comap_comap, Pi.uniformSpace_comap_restrict_sUnion (fun _ â†¦ Î±) ğ”–,
      UniformSpace.comap_iInf]
  -- But, for `K âˆˆ ğ”–` fixed, we know that the uniform structures of `K â†’áµ¤ Î±` and `K â†’ Î±`
  -- induce, via the equicontinuous family `K.restrict âˆ˜ F`, the same uniform structure on `Î¹`.
  have H3 : âˆ€ K âˆˆ ğ”–, (UniformFun.uniformSpace K Î±).comap (K.restrict âˆ˜ F) =
      (Pi.uniformSpace _).comap (K.restrict âˆ˜ F) := fun K hK â†¦ by
    have : CompactSpace K := isCompact_iff_compactSpace.mp (ğ”–_compact K hK)
    exact (equicontinuous_restrict_iff _ |>.mpr <| F_eqcont K hK).comap_uniformFun_eq
  -- Combining these three facts completes the proof.
  simp_rw [H1, H2, iInf_congr fun K â†¦ iInf_congr fun hK â†¦ H3 K hK]

/-- Let `X` be a topological space, `ğ”–` a family of compact subsets of `X`, `Î±` a uniform space,
and `F : Î¹ â†’ (X â†’ Î±)` a family which is equicontinuous on each `K âˆˆ ğ”–`. Then, the uniform
structures of uniform convergence on `ğ”–` and pointwise convergence on `â‹ƒâ‚€ ğ”–` induce the same
uniform structure on `Î¹`.

In particular, pointwise convergence and compact convergence coincide on an equicontinuous
subset of `X â†’ Î±`.

This is a version of `EquicontinuousOn.comap_uniformOnFun_eq` stated in terms of `IsUniformInducing`
for convenience. -/
lemma EquicontinuousOn.isUniformInducing_uniformOnFun_iff_pi' [UniformSpace Î¹]
    {ğ”– : Set (Set X)} (ğ”–_compact : âˆ€ K âˆˆ ğ”–, IsCompact K)
    (F_eqcont : âˆ€ K âˆˆ ğ”–, EquicontinuousOn F K) :
    IsUniformInducing (UniformOnFun.ofFun ğ”– âˆ˜ F) â†”
    IsUniformInducing ((â‹ƒâ‚€ ğ”–).restrict âˆ˜ F) := by
  rw [isUniformInducing_iff_uniformSpace, isUniformInducing_iff_uniformSpace,
      â† EquicontinuousOn.comap_uniformOnFun_eq ğ”–_compact F_eqcont]
  rfl

/-- Let `X` be a topological space, `ğ”–` a covering of `X` by compact subsets, `Î±` a uniform space,
and `F : Î¹ â†’ (X â†’ Î±)` a family which is equicontinuous on each `K âˆˆ ğ”–`. Then, the uniform
structures of uniform convergence on `ğ”–` and pointwise convergence induce the same
uniform structure on `Î¹`.

This is a specialization of `EquicontinuousOn.isUniformInducing_uniformOnFun_iff_pi'` to
the case where `ğ”–` covers `X`. -/
lemma EquicontinuousOn.isUniformInducing_uniformOnFun_iff_pi [UniformSpace Î¹]
    {ğ”– : Set (Set X)} (ğ”–_covers : â‹ƒâ‚€ ğ”– = univ) (ğ”–_compact : âˆ€ K âˆˆ ğ”–, IsCompact K)
    (F_eqcont : âˆ€ K âˆˆ ğ”–, EquicontinuousOn F K) :
    IsUniformInducing (UniformOnFun.ofFun ğ”– âˆ˜ F) â†”
    IsUniformInducing F := by
  rw [eq_univ_iff_forall] at ğ”–_covers
  -- This obviously follows from the previous lemma, we formalize it by going through the
  -- isomorphism of uniform spaces between `(â‹ƒâ‚€ ğ”–) â†’ Î±` and `X â†’ Î±`.
  let Ï† : ((â‹ƒâ‚€ ğ”–) â†’ Î±) â‰ƒáµ¤ (X â†’ Î±) := UniformEquiv.piCongrLeft (Î² := fun _ â†¦ Î±)
    (Equiv.subtypeUnivEquiv ğ”–_covers)
  rw [EquicontinuousOn.isUniformInducing_uniformOnFun_iff_pi' ğ”–_compact F_eqcont,
      show restrict (â‹ƒâ‚€ ğ”–) âˆ˜ F = Ï†.symm âˆ˜ F by rfl]
  exact âŸ¨fun H â†¦ Ï†.isUniformInducing.comp H, fun H â†¦ Ï†.symm.isUniformInducing.comp HâŸ©

/-- Let `X` be a topological space, `ğ”–` a family of compact subsets of `X`, `Î±` a uniform space,
and `F : Î¹ â†’ (X â†’ Î±)` a family which is equicontinuous on each `K âˆˆ ğ”–`. Then, the topologies
of uniform convergence on `ğ”–` and pointwise convergence on `â‹ƒâ‚€ ğ”–` induce the same topology on  `Î¹`.

In particular, pointwise convergence and compact convergence coincide on an equicontinuous
subset of `X â†’ Î±`.

This is a consequence of `EquicontinuousOn.comap_uniformOnFun_eq` stated in terms of `IsInducing`
for convenience. -/
lemma EquicontinuousOn.inducing_uniformOnFun_iff_pi' [TopologicalSpace Î¹]
    {ğ”– : Set (Set X)} (ğ”–_compact : âˆ€ K âˆˆ ğ”–, IsCompact K)
    (F_eqcont : âˆ€ K âˆˆ ğ”–, EquicontinuousOn F K) :
    IsInducing (UniformOnFun.ofFun ğ”– âˆ˜ F) â†”
    IsInducing ((â‹ƒâ‚€ ğ”–).restrict âˆ˜ F) := by
  rw [isInducing_iff, isInducing_iff]
  change (_ = ((UniformOnFun.uniformSpace X Î± ğ”–).comap F).toTopologicalSpace) â†”
    (_ = ((Pi.uniformSpace _).comap ((â‹ƒâ‚€ ğ”–).restrict âˆ˜ F)).toTopologicalSpace)
  rw [â† EquicontinuousOn.comap_uniformOnFun_eq ğ”–_compact F_eqcont]

/-- Let `X` be a topological space, `ğ”–` a covering of `X` by compact subsets, `Î±` a uniform space,
and `F : Î¹ â†’ (X â†’ Î±)` a family which is equicontinuous on each `K âˆˆ ğ”–`. Then, the topologies
of uniform convergence on `ğ”–` and pointwise convergence induce the same topology on `Î¹`.

This is a specialization of `EquicontinuousOn.inducing_uniformOnFun_iff_pi'` to
the case where `ğ”–` covers `X`. -/
lemma EquicontinuousOn.isInducing_uniformOnFun_iff_pi [TopologicalSpace Î¹]
    {ğ”– : Set (Set X)} (ğ”–_covers : â‹ƒâ‚€ ğ”– = univ) (ğ”–_compact : âˆ€ K âˆˆ ğ”–, IsCompact K)
    (F_eqcont : âˆ€ K âˆˆ ğ”–, EquicontinuousOn F K) :
    IsInducing (UniformOnFun.ofFun ğ”– âˆ˜ F) â†”
    IsInducing F := by
  rw [eq_univ_iff_forall] at ğ”–_covers
  -- This obviously follows from the previous lemma, we formalize it by going through the
  -- homeomorphism between `(â‹ƒâ‚€ ğ”–) â†’ Î±` and `X â†’ Î±`.
  let Ï† : ((â‹ƒâ‚€ ğ”–) â†’ Î±) â‰ƒâ‚œ (X â†’ Î±) := Homeomorph.piCongrLeft (Y := fun _ â†¦ Î±)
    (Equiv.subtypeUnivEquiv ğ”–_covers)
  rw [EquicontinuousOn.inducing_uniformOnFun_iff_pi' ğ”–_compact F_eqcont,
      show restrict (â‹ƒâ‚€ ğ”–) âˆ˜ F = Ï†.symm âˆ˜ F by rfl]
  exact âŸ¨fun H â†¦ Ï†.isInducing.comp H, fun H â†¦ Ï†.symm.isInducing.comp HâŸ©

-- TODO: find a way to factor common elements of this proof and the proof of
-- `EquicontinuousOn.comap_uniformOnFun_eq`
/-- Let `X` be a topological space, `ğ”–` a family of compact subsets of `X`,
`Î±` a uniform space, `F : Î¹ â†’ (X â†’ Î±)` a family equicontinuous on each `K âˆˆ ğ”–`, and `â„±` a filter
on `Î¹`. Then, `F` tends to `f : X â†’ Î±` along `â„±` *uniformly on each `K âˆˆ ğ”–`* iff it tends to `f`
*pointwise on `â‹ƒâ‚€ ğ”–`* along `â„±`. -/
theorem EquicontinuousOn.tendsto_uniformOnFun_iff_pi'
    {ğ”– : Set (Set X)} (ğ”–_compact : âˆ€ K âˆˆ ğ”–, IsCompact K)
    (F_eqcont : âˆ€ K âˆˆ ğ”–, EquicontinuousOn F K) (â„± : Filter Î¹) (f : X â†’ Î±) :
    Tendsto (UniformOnFun.ofFun ğ”– âˆ˜ F) â„± (ğ“ <| UniformOnFun.ofFun ğ”– f) â†”
    Tendsto ((â‹ƒâ‚€ ğ”–).restrict âˆ˜ F) â„± (ğ“ <| (â‹ƒâ‚€ ğ”–).restrict f) := by
  -- Recall that the uniform structure on `X â†’áµ¤[ğ”–] Î±` is the one induced by all the maps
  -- `K.restrict : (X â†’áµ¤[ğ”–] Î±) â†’ (K â†’áµ¤ Î±)` for `K âˆˆ ğ”–`.
  -- Similarly, the uniform structure on `X â†’ Î±` induced by the map
  -- `(â‹ƒâ‚€ ğ”–).restrict : (X â†’ Î±) â†’ ((â‹ƒâ‚€ ğ”–) â†’ Î±)` is equal to the one induced by
  -- all maps `K.restrict : (X â†’ Î±) â†’ (K â†’ Î±)` for `K âˆˆ ğ”–`
  -- Thus, we just have to compare the two sides of our goal when restricted to some
  -- `K âˆˆ ğ”–`, where we can apply `Equicontinuous.tendsto_uniformFun_iff_pi`.
  rw [â† Filter.tendsto_comap_iff (g := (â‹ƒâ‚€ ğ”–).restrict), â† nhds_induced]
  simp_rw [UniformOnFun.topologicalSpace_eq, Pi.induced_restrict_sUnion ğ”– (A := fun _ â†¦ Î±),
    _root_.nhds_iInf, nhds_induced, tendsto_iInf, tendsto_comap_iff]
  congrm âˆ€ K (hK : K âˆˆ ğ”–), ?_
  have : CompactSpace K := isCompact_iff_compactSpace.mp (ğ”–_compact K hK)
  rw [â† (equicontinuous_restrict_iff _ |>.mpr <| F_eqcont K hK).tendsto_uniformFun_iff_pi]
  rfl

/-- Let `X` be a topological space, `ğ”–` a covering of `X` by compact subsets,
`Î±` a uniform space, `F : Î¹ â†’ (X â†’ Î±)` a family equicontinuous on each `K âˆˆ ğ”–`, and `â„±` a filter
on `Î¹`. Then, `F` tends to `f : X â†’ Î±` along `â„±` *uniformly on each `K âˆˆ ğ”–`* iff it tends to `f`
*pointwise* along `â„±`.

This is a specialization of `EquicontinuousOn.tendsto_uniformOnFun_iff_pi'` to the case
where `ğ”–` covers `X`. -/
theorem EquicontinuousOn.tendsto_uniformOnFun_iff_pi
    {ğ”– : Set (Set X)} (ğ”–_compact : âˆ€ K âˆˆ ğ”–, IsCompact K) (ğ”–_covers : â‹ƒâ‚€ ğ”– = univ)
    (F_eqcont : âˆ€ K âˆˆ ğ”–, EquicontinuousOn F K) (â„± : Filter Î¹) (f : X â†’ Î±) :
    Tendsto (UniformOnFun.ofFun ğ”– âˆ˜ F) â„± (ğ“ <| UniformOnFun.ofFun ğ”– f) â†”
    Tendsto F â„± (ğ“ f) := by
  rw [eq_univ_iff_forall] at ğ”–_covers
  let Ï† : ((â‹ƒâ‚€ ğ”–) â†’ Î±) â‰ƒâ‚œ (X â†’ Î±) := Homeomorph.piCongrLeft (Y := fun _ â†¦ Î±)
    (Equiv.subtypeUnivEquiv ğ”–_covers)
  rw [EquicontinuousOn.tendsto_uniformOnFun_iff_pi' ğ”–_compact F_eqcont,
      show restrict (â‹ƒâ‚€ ğ”–) âˆ˜ F = Ï†.symm âˆ˜ F by rfl, show restrict (â‹ƒâ‚€ ğ”–) f = Ï†.symm f by rfl,
      Ï†.symm.isInducing.tendsto_nhds_iff]

/-- Let `X` be a topological space, `ğ”–` a family of compact subsets of `X` and
`Î±` a uniform space. An equicontinuous subset of `X â†’ Î±` is closed in the topology of uniform
convergence on all `K âˆˆ ğ”–` iff it is closed in the topology of pointwise convergence on `â‹ƒâ‚€ ğ”–`. -/
theorem EquicontinuousOn.isClosed_range_pi_of_uniformOnFun'
    {ğ”– : Set (Set X)} (ğ”–_compact : âˆ€ K âˆˆ ğ”–, IsCompact K)
    (F_eqcont : âˆ€ K âˆˆ ğ”–, EquicontinuousOn F K)
    (H : IsClosed (range <| UniformOnFun.ofFun ğ”– âˆ˜ F)) :
    IsClosed (range <| (â‹ƒâ‚€ ğ”–).restrict âˆ˜ F) := by
  -- Do we have no equivalent of `nontriviality`?
  rcases isEmpty_or_nonempty Î± with _ | _
  Â· simp [isClosed_discrete]
  -- This follows from the previous lemmas and the characterization of the closure using filters.
  simp_rw [isClosed_iff_clusterPt, â† Filter.map_top, â† mapClusterPt_def,
    mapClusterPt_iff_ultrafilter, range_comp, Subtype.coe_injective.surjective_comp_right.forall,
    â† restrict_eq, â† EquicontinuousOn.tendsto_uniformOnFun_iff_pi' ğ”–_compact F_eqcont]
  exact fun f âŸ¨u, _, huâŸ© â†¦ mem_image_of_mem _ <| H.mem_of_tendsto hu <|
    Eventually.of_forall mem_range_self

/-- Let `X` be a topological space, `ğ”–` a covering of `X` by compact subsets, and
`Î±` a uniform space. An equicontinuous subset of `X â†’ Î±` is closed in the topology of uniform
convergence on all `K âˆˆ ğ”–` iff it is closed in the topology of pointwise convergence.

This is a specialization of `EquicontinuousOn.isClosed_range_pi_of_uniformOnFun'` to the case where
`ğ”–` covers `X`. -/
theorem EquicontinuousOn.isClosed_range_uniformOnFun_iff_pi
    {ğ”– : Set (Set X)} (ğ”–_compact : âˆ€ K âˆˆ ğ”–, IsCompact K) (ğ”–_covers : â‹ƒâ‚€ ğ”– = univ)
    (F_eqcont : âˆ€ K âˆˆ ğ”–, EquicontinuousOn F K) :
    IsClosed (range <| UniformOnFun.ofFun ğ”– âˆ˜ F) â†”
    IsClosed (range F) := by
  -- This follows from the previous lemmas and the characterization of the closure using filters.
  simp_rw [isClosed_iff_clusterPt, â† Filter.map_top, â† mapClusterPt_def,
    mapClusterPt_iff_ultrafilter, range_comp, (UniformOnFun.ofFun ğ”–).surjective.forall,
    â† EquicontinuousOn.tendsto_uniformOnFun_iff_pi ğ”–_compact ğ”–_covers F_eqcont,
    (UniformOnFun.ofFun ğ”–).injective.mem_set_image]

alias âŸ¨EquicontinuousOn.isClosed_range_pi_of_uniformOnFun, _âŸ© :=
  EquicontinuousOn.isClosed_range_uniformOnFun_iff_pi

/-- A version of the **Arzela-Ascoli theorem**.

Let `X` be a topological space, `ğ”–` a family of compact subsets of `X`, `Î±` a uniform space,
and `F : Î¹ â†’ (X â†’ Î±)`. Assume that:
* `F`, viewed as a function `Î¹ â†’ (X â†’áµ¤[ğ”–] Î±)`, is closed and inducing
* `F` is equicontinuous on each `K âˆˆ ğ”–`
* For all `x âˆˆ â‹ƒâ‚€ ğ”–`, the range of `i â†¦ F i x` is contained in some fixed compact subset.

Then `Î¹` is compact. -/
theorem ArzelaAscoli.compactSpace_of_closed_inducing' [TopologicalSpace Î¹] {ğ”– : Set (Set X)}
    (ğ”–_compact : âˆ€ K âˆˆ ğ”–, IsCompact K) (F_ind : IsInducing (UniformOnFun.ofFun ğ”– âˆ˜ F))
    (F_cl : IsClosed <| range <| UniformOnFun.ofFun ğ”– âˆ˜ F)
    (F_eqcont : âˆ€ K âˆˆ ğ”–, EquicontinuousOn F K)
    (F_pointwiseCompact : âˆ€ K âˆˆ ğ”–, âˆ€ x âˆˆ K, âˆƒ Q, IsCompact Q âˆ§ âˆ€ i, F i x âˆˆ Q) :
    CompactSpace Î¹ := by
  -- By equicontinuity, we know that the topology on `Î¹` is also the one induced by
  -- `restrict (â‹ƒâ‚€ ğ”–) âˆ˜ F`.
  have : IsInducing (restrict (â‹ƒâ‚€ ğ”–) âˆ˜ F) := by
    rwa [EquicontinuousOn.inducing_uniformOnFun_iff_pi' ğ”–_compact F_eqcont] at F_ind
  -- Thus, we just have to check that the range of this map is compact.
  rw [â† isCompact_univ_iff, this.isCompact_iff, image_univ]
  -- But then we are working in a product space, where compactness can easily be proven using
  -- Tykhonov's theorem! More precisely, for each `x âˆˆ â‹ƒâ‚€ ğ”–`, choose a compact set `Q x`
  -- containing all `F i x`s.
  rw [â† forall_sUnion] at F_pointwiseCompact
  choose! Q Q_compact F_in_Q using F_pointwiseCompact
  -- Notice that, since the range of `F` is closed in `X â†’áµ¤[ğ”–] Î±`, equicontinuity ensures that
  -- the range of `(â‹ƒâ‚€ ğ”–).restrict âˆ˜ F` is still closed in the product topology.
  -- But it's contained in the product of the `Q x`s, which is compact by Tykhonov, hence
  -- it is compact as well.
  refine IsCompact.of_isClosed_subset (isCompact_univ_pi fun x â†¦ Q_compact x x.2)
    (EquicontinuousOn.isClosed_range_pi_of_uniformOnFun' ğ”–_compact F_eqcont F_cl)
    (range_subset_iff.mpr fun i x _ â†¦ F_in_Q x x.2 i)

/-- A version of the **Arzela-Ascoli theorem**.

Let `X, Î¹` be topological spaces, `ğ”–` a covering of `X` by compact subsets, `Î±` a uniform space,
and `F : Î¹ â†’ (X â†’ Î±)`. Assume that:
* `F`, viewed as a function `Î¹ â†’ (X â†’áµ¤[ğ”–] Î±)`, is a closed embedding (in other words, `Î¹`
  identifies to a closed subset of `X â†’áµ¤[ğ”–] Î±` through `F`)
* `F` is equicontinuous on each `K âˆˆ ğ”–`
* For all `x`, the range of `i â†¦ F i x` is contained in some fixed compact subset.

Then `Î¹` is compact. -/
theorem ArzelaAscoli.compactSpace_of_isClosedEmbedding [TopologicalSpace Î¹] {ğ”– : Set (Set X)}
    (ğ”–_compact : âˆ€ K âˆˆ ğ”–, IsCompact K) (F_clemb : IsClosedEmbedding (UniformOnFun.ofFun ğ”– âˆ˜ F))
    (F_eqcont : âˆ€ K âˆˆ ğ”–, EquicontinuousOn F K)
    (F_pointwiseCompact : âˆ€ K âˆˆ ğ”–, âˆ€ x âˆˆ K, âˆƒ Q, IsCompact Q âˆ§ âˆ€ i, F i x âˆˆ Q) :
    CompactSpace Î¹ :=
  compactSpace_of_closed_inducing' ğ”–_compact F_clemb.isInducing F_clemb.isClosed_range
    F_eqcont F_pointwiseCompact

/-- A version of the **Arzela-Ascoli theorem**.

Let `X, Î¹` be topological spaces, `ğ”–` a covering of `X` by compact subsets, `Î±` a T2 uniform space,
`F : Î¹ â†’ (X â†’ Î±)`, and `s` a subset of `Î¹`. Assume that:
* `F`, viewed as a function `Î¹ â†’ (X â†’áµ¤[ğ”–] Î±)`, is a closed embedding (in other words, `Î¹`
  identifies to a closed subset of `X â†’áµ¤[ğ”–] Î±` through `F`)
* `F '' s` is equicontinuous on each `K âˆˆ ğ”–`
* For all `x âˆˆ â‹ƒâ‚€ ğ”–`, the image of `s` under `i â†¦ F i x` is contained in some fixed compact subset.

Then `s` has compact closure in `Î¹`. -/
theorem ArzelaAscoli.isCompact_closure_of_isClosedEmbedding [TopologicalSpace Î¹] [T2Space Î±]
    {ğ”– : Set (Set X)} (ğ”–_compact : âˆ€ K âˆˆ ğ”–, IsCompact K)
    (F_clemb : IsClosedEmbedding (UniformOnFun.ofFun ğ”– âˆ˜ F))
    {s : Set Î¹} (s_eqcont : âˆ€ K âˆˆ ğ”–, EquicontinuousOn (F âˆ˜ ((â†‘) : s â†’ Î¹)) K)
    (s_pointwiseCompact : âˆ€ K âˆˆ ğ”–, âˆ€ x âˆˆ K, âˆƒ Q, IsCompact Q âˆ§ âˆ€ i âˆˆ s, F i x âˆˆ Q) :
    IsCompact (closure s) := by
  -- We apply `ArzelaAscoli.compactSpace_of_isClosedEmbedding` to the map
  -- `F âˆ˜ (â†‘) : closure s â†’ (X â†’ Î±)`, for which all the hypotheses are easily verified.
  rw [isCompact_iff_compactSpace]
  have : âˆ€ K âˆˆ ğ”–, âˆ€ x âˆˆ K, Continuous (eval x âˆ˜ F) := fun K hK x hx â†¦
    UniformOnFun.uniformContinuous_eval_of_mem _ _ hx hK |>.continuous.comp F_clemb.continuous
  have cls_eqcont : âˆ€ K âˆˆ ğ”–, EquicontinuousOn (F âˆ˜ ((â†‘) : closure s â†’ Î¹)) K :=
    fun K hK â†¦ (s_eqcont K hK).closure' <| show Continuous (K.restrict âˆ˜ F) from
      continuous_pi fun âŸ¨x, hxâŸ© â†¦ this K hK x hx
  have cls_pointwiseCompact : âˆ€ K âˆˆ ğ”–, âˆ€ x âˆˆ K, âˆƒ Q, IsCompact Q âˆ§ âˆ€ i âˆˆ closure s, F i x âˆˆ Q :=
    fun K hK x hx â†¦ (s_pointwiseCompact K hK x hx).imp fun Q hQ â†¦ âŸ¨hQ.1, closure_minimal hQ.2 <|
      hQ.1.isClosed.preimage (this K hK x hx)âŸ©
  exact ArzelaAscoli.compactSpace_of_isClosedEmbedding ğ”–_compact
    (F_clemb.comp isClosed_closure.isClosedEmbedding_subtypeVal) cls_eqcont
    fun K hK x hx â†¦ (cls_pointwiseCompact K hK x hx).imp fun Q hQ â†¦ âŸ¨hQ.1, by simpa using hQ.2âŸ©

/-- A version of the **Arzela-Ascoli theorem**.

If an equicontinuous family of continuous functions is compact in the pointwise topology, then it
is compact in the compact open topology. -/
theorem ArzelaAscoli.isCompact_of_equicontinuous
    (S : Set C(X, Î±)) (hS1 : IsCompact (ContinuousMap.toFun '' S))
    (hS2 : Equicontinuous ((â†‘) : S â†’ X â†’ Î±)) : IsCompact S := by
  suffices h : IsInducing (Equiv.Set.image _ S DFunLike.coe_injective) by
    rw [isCompact_iff_compactSpace] at hS1 âŠ¢
    exact (Equiv.toHomeomorphOfIsInducing _ h).symm.compactSpace
  rw [â† IsInducing.subtypeVal.of_comp_iff, â† EquicontinuousOn.isInducing_uniformOnFun_iff_pi _ _ _]
  Â· exact ContinuousMap.isUniformEmbedding_toUniformOnFunIsCompact.isInducing.comp .subtypeVal
  Â· exact eq_univ_iff_forall.mpr (fun x â†¦ mem_sUnion_of_mem (mem_singleton x) isCompact_singleton)
  Â· exact fun _ â†¦ id
  Â· exact fun K _ â†¦ hS2.equicontinuousOn K
