/-
Copyright (c) 2017 Yakov Pechersky. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yakov Pechersky
-/
import Mathlib.Topology.UniformSpace.Basic
import Mathlib.Topology.Bases

/-!
# Ultrametric (nonarchimedean) uniform spaces

Ultrametric (nonarchimedean) uniform spaces are ones that generalize ultrametric spaces by
having a uniformity based on equivalence relations.

## Main definitions

In this file we define `IsUltraUniformity`, a Prop mixin typeclass.

## Main results

* `TopologicalSpace.isTopologicalBasis_clopens`: a uniform space with a nonarchimedean uniformity
  has a topological basis of clopen sets in the topology, meaning that it is topologically
  zero-dimensional.

## Implementation notes

As in the `Topology/UniformSpace/Defs.lean` file, we do not reuse `Data/Rel.lean`
but rather extend the relation properties as needed.

## TODOs

* Prove that `IsUltraUniformity` iff `IsUltrametricDist` on a `PseudoMetricSpace`
* Generalize `IsUltrametricDist` to `IsUltrametricUniformity`
* Provide `IsUltraUniformity` for the uniformity in a `Valued` ring
* Generalize results about open/closed balls and spheres in `IsUltraUniformity` to
  combine applications for `MetricSpace.ball` and valued "balls"
* Use `IsUltraUniformity` to work with profinite/totally separated spaces
* Define the nonarchimedean uniformity of a space that is a product of `IsUltraUniformity`s
* Show that the `UniformSpace.Completion` of an `IsUltraUniformity` is `IsUltraUniformity`

## References

* [D. Windisch, *Equivalent characterizations of non-Archimedean uniform spaces*][windisch2021]
* [A. C. M. van Rooij, *Non-Archimedean uniformities*][vanrooij1970]

-/

open Set Filter Topology
open scoped Uniformity

variable {X : Type*}

/-- The relation is transitive. -/
def TransitiveRel (V : Set (X Ã— X)) : Prop :=
  âˆ€ â¦ƒx y zâ¦„, (x, y) âˆˆ V â†’ (y, z) âˆˆ V â†’ (x, z) âˆˆ V

lemma TransitiveRel.comp_eq_of_idRel_subset {s : Set (X Ã— X)}
    (h : TransitiveRel s) (h' : idRel âŠ† s) :
    s â—‹ s = s := by
  refine le_antisymm ?_ (subset_comp_self h')
  intro âŸ¨x, yâŸ©
  simp only [mem_compRel, forall_exists_index, and_imp]
  intro z
  exact @h x z y

variable [UniformSpace X]

variable (X) in
/-- A uniform space is ultrametric if the uniformity `ğ“¤ X` has a basis of equivalence relations. -/
class IsUltraUniformity : Prop where
  has_basis : (ğ“¤ X).HasBasis
    (fun s : Set (X Ã— X) => s âˆˆ ğ“¤ X âˆ§ SymmetricRel s âˆ§ TransitiveRel s) id

variable [IsUltraUniformity X]

namespace UniformSpace

lemma nhds_basis_clopens (x : X) :
    (ğ“ x).HasBasis (fun s : Set X => x âˆˆ s âˆ§ IsClopen s) id := by
  constructor
  intro t
  constructor
  Â· rw [nhds_eq_comap_uniformity, Filter.mem_comap]
    rintro âŸ¨u, hu, hu'âŸ©
    rw [IsUltraUniformity.has_basis.mem_iff] at hu
    obtain âŸ¨v, hv, hv'âŸ© := hu
    refine âŸ¨{y | (x, y) âˆˆ v}, âŸ¨?_, ?_âŸ©, ?_âŸ©
    Â· simp only [mem_setOf_eq]
      exact refl_mem_uniformity hv.left
    Â· constructor
      Â· rw [â† isOpen_compl_iff, isOpen_uniformity]
        simp only [mem_compl_iff, mem_setOf_eq]
        intro z hz
        rw [IsUltraUniformity.has_basis.mem_iff]
        refine âŸ¨v, hv, ?_âŸ©
        intro âŸ¨a, bâŸ©
        simp only [id_eq, mem_setOf_eq]
        rintro h rfl H
        rw [hv.right.left.mk_mem_comm] at h
        exact hz (hv.right.right H h)
      Â· rw [isOpen_uniformity]
        simp only [mem_setOf_eq]
        intro z hz
        rw [IsUltraUniformity.has_basis.mem_iff]
        refine âŸ¨v, hv, ?_âŸ©
        intro âŸ¨a, bâŸ©
        simp only [id_eq, mem_setOf_eq]
        rintro h rfl
        exact hv.right.right hz h
    Â· refine le_trans ?_ hu'
      intro z
      simp only [id_eq, mem_setOf_eq, mem_preimage]
      intro hz
      exact hv' hz
  Â· rintro âŸ¨u, âŸ¨hu, hu'âŸ©, hu''âŸ©
    rw [_root_.mem_nhds_iff]
    exact âŸ¨u, hu'', hu'.right, huâŸ©

/-- A uniform space with a nonarchimedean uniformity is zero-dimensional. -/
lemma _root_.TopologicalSpace.isTopologicalBasis_clopens :
    TopologicalSpace.IsTopologicalBasis {s : Set X | IsClopen s} := by
  apply TopologicalSpace.isTopologicalBasis_of_isOpen_of_nhds fun U (hU : IsClopen U) => hU.2
  intro x U hxU U_op
  have : U âˆˆ ğ“ x := IsOpen.mem_nhds U_op hxU
  rcases (nhds_basis_clopens x).mem_iff.mp this with âŸ¨V, âŸ¨hxV, hVâŸ©, hVU : V âŠ† UâŸ©
  use V
  tauto

end UniformSpace
