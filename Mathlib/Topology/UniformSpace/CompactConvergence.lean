/-
Copyright (c) 2021 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import Mathlib.Topology.CompactOpen
import Mathlib.Topology.UniformSpace.UniformConvergence

#align_import topology.uniform_space.compact_convergence from "leanprover-community/mathlib"@"dc6c365e751e34d100e80fe6e314c3c3e0fd2988"

/-!
# Compact convergence (uniform convergence on compact sets)

Given a topological space `Œ±` and a uniform space `Œ≤` (e.g., a metric space or a topological group),
the space of continuous maps `C(Œ±, Œ≤)` carries a natural uniform space structure. We define this
uniform space structure in this file and also prove the following properties of the topology it
induces on `C(Œ±, Œ≤)`:

 1. Given a sequence of continuous functions `F‚Çô : Œ± ‚Üí Œ≤` together with some continuous `f : Œ± ‚Üí Œ≤`,
    then `F‚Çô` converges to `f` as a sequence in `C(Œ±, Œ≤)` iff `F‚Çô` converges to `f` uniformly on
    each compact subset `K` of `Œ±`.
 2. Given `F‚Çô` and `f` as above and suppose `Œ±` is locally compact, then `F‚Çô` converges to `f` iff
    `F‚Çô` converges to `f` locally uniformly.
 3. The topology coincides with the compact-open topology.

Property 1 is essentially true by definition, 2 follows from basic results about uniform
convergence, but 3 requires a little work and uses the Lebesgue number lemma.

## The uniform space structure

Given subsets `K ‚äÜ Œ±` and `V ‚äÜ Œ≤ √ó Œ≤`, let `E(K, V) ‚äÜ C(Œ±, Œ≤) √ó C(Œ±, Œ≤)` be the set of pairs of
continuous functions `Œ± ‚Üí Œ≤` which are `V`-close on `K`:
$$
  E(K, V) = \{ (f, g) | ‚àÄ (x ‚àà K), (f x, g x) ‚àà V \}.
$$
Fixing some `f ‚àà C(Œ±, Œ≤)`, let `N(K, V, f) ‚äÜ C(Œ±, Œ≤)` be the set of continuous functions `Œ± ‚Üí Œ≤`
which are `V`-close to `f` on `K`:
$$
  N(K, V, f) = \{ g | ‚àÄ (x ‚àà K), (f x, g x) ‚àà V \}.
$$
Using this notation we can describe the uniform space structure and the topology it induces.
Specifically:
  * A subset `X ‚äÜ C(Œ±, Œ≤) √ó C(Œ±, Œ≤)` is an entourage for the uniform space structure on `C(Œ±, Œ≤)`
    iff there exists a compact `K` and entourage `V` such that `E(K, V) ‚äÜ X`.
  * A subset `Y ‚äÜ C(Œ±, Œ≤)` is a neighbourhood of `f` iff there exists a compact `K` and entourage
    `V` such that `N(K, V, f) ‚äÜ Y`.

The topology on `C(Œ±, Œ≤)` thus has a natural subbasis (the compact-open subbasis) and a natural
neighbourhood basis (the compact-convergence neighbourhood basis).

## Main definitions / results

 * `ContinuousMap.compactOpen_eq_compactConvergence`: the compact-open topology is equal to the
   compact-convergence topology.
 * `ContinuousMap.compactConvergenceUniformSpace`: the uniform space structure on `C(Œ±, Œ≤)`.
 * `ContinuousMap.mem_compactConvergence_entourage_iff`: a characterisation of the entourages
    of `C(Œ±, Œ≤)`.
 * `ContinuousMap.tendsto_iff_forall_compact_tendstoUniformlyOn`: a sequence of functions `F‚Çô` in
    `C(Œ±, Œ≤)` converges to some `f` iff `F‚Çô` converges to `f` uniformly on each compact subset
    `K` of `Œ±`.
 * `ContinuousMap.tendsto_iff_tendstoLocallyUniformly`: on a locally compact space, a sequence of
    functions `F‚Çô` in `C(Œ±, Œ≤)` converges to some `f` iff `F‚Çô` converges to `f` locally uniformly.
 * `ContinuousMap.tendsto_iff_tendstoUniformly`: on a compact space, a sequence of functions `F‚Çô` in
    `C(Œ±, Œ≤)` converges to some `f` iff `F‚Çô` converges to `f` uniformly.

## Implementation details

We use the forgetful inheritance pattern (see Note [forgetful inheritance]) to make the topology
of the uniform space structure on `C(Œ±, Œ≤)` definitionally equal to the compact-open topology.

## TODO

 * When `Œ≤` is a metric space, there is natural basis for the compact-convergence topology
   parameterised by triples `(K, Œµ, f)` for a real number `Œµ > 0`.
 * When `Œ±` is compact and `Œ≤` is a metric space, the compact-convergence topology (and thus also
   the compact-open topology) is metrisable.
 * Results about uniformly continuous functions `Œ≥ ‚Üí C(Œ±, Œ≤)` and uniform limits of sequences
   `Œπ ‚Üí Œ≥ ‚Üí C(Œ±, Œ≤)`.
-/


universe u‚ÇÅ u‚ÇÇ u‚ÇÉ

open Filter Uniformity Topology

open UniformSpace Set Filter

variable {Œ± : Type u‚ÇÅ} {Œ≤ : Type u‚ÇÇ} [TopologicalSpace Œ±] [UniformSpace Œ≤]

variable (K : Set Œ±) (V : Set (Œ≤ √ó Œ≤)) (f : C(Œ±, Œ≤))

namespace ContinuousMap

/-- Given `K ‚äÜ Œ±`, `V ‚äÜ Œ≤ √ó Œ≤`, and `f : C(Œ±, Œ≤)`, we define `ContinuousMap.compactConvNhd K V f`
to be the set of `g : C(Œ±, Œ≤)` that are `V`-close to `f` on `K`. -/
def compactConvNhd : Set C(Œ±, Œ≤) :=
  { g | ‚àÄ x ‚àà K, (f x, g x) ‚àà V }
#align continuous_map.compact_conv_nhd ContinuousMap.compactConvNhd

variable {K V}

theorem self_mem_compactConvNhd (hV : V ‚àà ùì§ Œ≤) : f ‚àà compactConvNhd K V f := fun _x _hx =>
  refl_mem_uniformity hV
#align continuous_map.self_mem_compact_conv_nhd ContinuousMap.self_mem_compactConvNhd

@[mono]
theorem compactConvNhd_mono {V' : Set (Œ≤ √ó Œ≤)} (hV' : V' ‚äÜ V) :
    compactConvNhd K V' f ‚äÜ compactConvNhd K V f := fun _x hx a ha => hV' (hx a ha)
#align continuous_map.compact_conv_nhd_mono ContinuousMap.compactConvNhd_mono

theorem compactConvNhd_mem_comp {g‚ÇÅ g‚ÇÇ : C(Œ±, Œ≤)} {V' : Set (Œ≤ √ó Œ≤)}
    (hg‚ÇÅ : g‚ÇÅ ‚àà compactConvNhd K V f) (hg‚ÇÇ : g‚ÇÇ ‚àà compactConvNhd K V' g‚ÇÅ) :
    g‚ÇÇ ‚àà compactConvNhd K (V ‚óã V') f := fun x hx => ‚ü®g‚ÇÅ x, hg‚ÇÅ x hx, hg‚ÇÇ x hx‚ü©
#align continuous_map.compact_conv_nhd_mem_comp ContinuousMap.compactConvNhd_mem_comp

/-- A key property of `ContinuousMap.compactConvNhd`. It allows us to apply
`TopologicalSpace.nhds_mkOfNhds_filterBasis` below. -/
theorem compactConvNhd_nhd_basis (hV : V ‚àà ùì§ Œ≤) :
    ‚àÉ V' ‚àà ùì§ Œ≤,
      V' ‚äÜ V ‚àß ‚àÄ g ‚àà compactConvNhd K V' f, compactConvNhd K V' g ‚äÜ compactConvNhd K V f := by
  obtain ‚ü®V', h‚ÇÅ, h‚ÇÇ‚ü© := comp_mem_uniformity_sets hV
  exact
    ‚ü®V', h‚ÇÅ, Subset.trans (subset_comp_self_of_mem_uniformity h‚ÇÅ) h‚ÇÇ, fun g hg g' hg' =>
      compactConvNhd_mono f h‚ÇÇ (compactConvNhd_mem_comp f hg hg')‚ü©
#align continuous_map.compact_conv_nhd_nhd_basis ContinuousMap.compactConvNhd_nhd_basis

theorem compactConvNhd_subset_inter (K‚ÇÅ K‚ÇÇ : Set Œ±) (V‚ÇÅ V‚ÇÇ : Set (Œ≤ √ó Œ≤)) :
    compactConvNhd (K‚ÇÅ ‚à™ K‚ÇÇ) (V‚ÇÅ ‚à© V‚ÇÇ) f ‚äÜ compactConvNhd K‚ÇÅ V‚ÇÅ f ‚à© compactConvNhd K‚ÇÇ V‚ÇÇ f :=
  fun _g hg =>
  ‚ü®fun x hx => mem_of_mem_inter_left (hg x (mem_union_left K‚ÇÇ hx)), fun x hx =>
    mem_of_mem_inter_right (hg x (mem_union_right K‚ÇÅ hx))‚ü©
#align continuous_map.compact_conv_nhd_subset_inter ContinuousMap.compactConvNhd_subset_inter

theorem compactConvNhd_compact_entourage_nonempty :
    { KV : Set Œ± √ó Set (Œ≤ √ó Œ≤) | IsCompact KV.1 ‚àß KV.2 ‚àà ùì§ Œ≤ }.Nonempty :=
  ‚ü®‚ü®‚àÖ, univ‚ü©, isCompact_empty, Filter.univ_mem‚ü©
#align continuous_map.compact_conv_nhd_compact_entourage_nonempty ContinuousMap.compactConvNhd_compact_entourage_nonempty

theorem compactConvNhd_filter_isBasis :
    Filter.IsBasis (fun KV : Set Œ± √ó Set (Œ≤ √ó Œ≤) => IsCompact KV.1 ‚àß KV.2 ‚àà ùì§ Œ≤) fun KV =>
      compactConvNhd KV.1 KV.2 f :=
  { nonempty := compactConvNhd_compact_entourage_nonempty
    inter := by
      rintro ‚ü®K‚ÇÅ, V‚ÇÅ‚ü© ‚ü®K‚ÇÇ, V‚ÇÇ‚ü© ‚ü®hK‚ÇÅ, hV‚ÇÅ‚ü© ‚ü®hK‚ÇÇ, hV‚ÇÇ‚ü©
      exact
        ‚ü®‚ü®K‚ÇÅ ‚à™ K‚ÇÇ, V‚ÇÅ ‚à© V‚ÇÇ‚ü©, ‚ü®hK‚ÇÅ.union hK‚ÇÇ, Filter.inter_mem hV‚ÇÅ hV‚ÇÇ‚ü©,
          compactConvNhd_subset_inter f K‚ÇÅ K‚ÇÇ V‚ÇÅ V‚ÇÇ‚ü© }
#align continuous_map.compact_conv_nhd_filter_is_basis ContinuousMap.compactConvNhd_filter_isBasis

/-- A filter basis for the neighbourhood filter of a point in the compact-convergence topology. -/
def compactConvergenceFilterBasis (f : C(Œ±, Œ≤)) : FilterBasis C(Œ±, Œ≤) :=
  (compactConvNhd_filter_isBasis f).filterBasis
#align continuous_map.compact_convergence_filter_basis ContinuousMap.compactConvergenceFilterBasis

theorem mem_compactConvergence_nhd_filter (Y : Set C(Œ±, Œ≤)) :
    Y ‚àà (compactConvergenceFilterBasis f).filter ‚Üî
    ‚àÉ (K : Set Œ±) (V : Set (Œ≤ √ó Œ≤)) (_hK : IsCompact K) (_hV : V ‚àà ùì§ Œ≤),
      compactConvNhd K V f ‚äÜ Y := by
  constructor
  ¬∑ rintro ‚ü®X, ‚ü®‚ü®K, V‚ü©, ‚ü®hK, hV‚ü©, rfl‚ü©, hY‚ü©
    exact ‚ü®K, V, hK, hV, hY‚ü©
  ¬∑ rintro ‚ü®K, V, hK, hV, hY‚ü©
    exact ‚ü®compactConvNhd K V f, ‚ü®‚ü®K, V‚ü©, ‚ü®hK, hV‚ü©, rfl‚ü©, hY‚ü©
#align continuous_map.mem_compact_convergence_nhd_filter ContinuousMap.mem_compactConvergence_nhd_filter

/-- The compact-convergence topology. In fact, see `ContinuousMap.compactOpen_eq_compactConvergence`
this is the same as the compact-open topology. This definition is thus an auxiliary convenience
definition and is unlikely to be of direct use. -/
def compactConvergenceTopology : TopologicalSpace C(Œ±, Œ≤) :=
  TopologicalSpace.mkOfNhds fun f => (compactConvergenceFilterBasis f).filter
#align continuous_map.compact_convergence_topology ContinuousMap.compactConvergenceTopology

theorem nhds_compactConvergence :
    @nhds _ compactConvergenceTopology f = (compactConvergenceFilterBasis f).filter := by
  rw [TopologicalSpace.nhds_mkOfNhds_filterBasis] <;> rintro g - ‚ü®‚ü®K, V‚ü©, ‚ü®hK, hV‚ü©, rfl‚ü©
  ¬∑ exact self_mem_compactConvNhd g hV
  ¬∑ obtain ‚ü®V', hV', h‚ÇÅ, h‚ÇÇ‚ü© := compactConvNhd_nhd_basis g hV
    exact
      ‚ü®compactConvNhd K V' g, ‚ü®‚ü®K, V'‚ü©, ‚ü®hK, hV'‚ü©, rfl‚ü©, compactConvNhd_mono g h‚ÇÅ, fun g' hg' =>
        ‚ü®compactConvNhd K V' g', ‚ü®‚ü®K, V'‚ü©, ‚ü®hK, hV'‚ü©, rfl‚ü©, h‚ÇÇ g' hg'‚ü©‚ü©
#align continuous_map.nhds_compact_convergence ContinuousMap.nhds_compactConvergence

theorem hasBasis_nhds_compactConvergence :
    HasBasis (@nhds _ compactConvergenceTopology f)
      (fun p : Set Œ± √ó Set (Œ≤ √ó Œ≤) => IsCompact p.1 ‚àß p.2 ‚àà ùì§ Œ≤) fun p =>
      compactConvNhd p.1 p.2 f :=
  (nhds_compactConvergence f).symm ‚ñ∏ (compactConvNhd_filter_isBasis f).hasBasis
#align continuous_map.has_basis_nhds_compact_convergence ContinuousMap.hasBasis_nhds_compactConvergence

/-- This is an auxiliary lemma and is unlikely to be of direct use outside of this file. See
`ContinuousMap.tendsto_iff_forall_compact_tendstoUniformlyOn` below for the useful version where the
topology is picked up via typeclass inference. -/
theorem tendsto_iff_forall_compact_tendstoUniformlyOn' {Œπ : Type u‚ÇÉ} {p : Filter Œπ}
    {F : Œπ ‚Üí C(Œ±, Œ≤)} :
    Filter.Tendsto F p (@nhds _ compactConvergenceTopology f) ‚Üî
      ‚àÄ K, IsCompact K ‚Üí TendstoUniformlyOn (fun i a => F i a) f p K := by
  simp only [(hasBasis_nhds_compactConvergence f).tendsto_right_iff, TendstoUniformlyOn, and_imp,
    Prod.forall]
  refine' forall_congr' fun K => _
  rw [forall_swap]
  exact forall‚ÇÉ_congr fun _hK V _hV => Iff.rfl
#align continuous_map.tendsto_iff_forall_compact_tendsto_uniformly_on' ContinuousMap.tendsto_iff_forall_compact_tendstoUniformlyOn'

/-- Any point of `ContinuousMap.CompactOpen.gen K U` is also an interior point wrt the topology of
compact convergence.

The topology of compact convergence is thus at least as fine as the compact-open topology. -/
theorem compactConvNhd_subset_compactOpen (hK : IsCompact K) {U : Set Œ≤} (hU : IsOpen U)
    (hf : f ‚àà CompactOpen.gen K U) :
    ‚àÉ V ‚àà ùì§ Œ≤, IsOpen V ‚àß compactConvNhd K V f ‚äÜ CompactOpen.gen K U := by
  obtain ‚ü®V, hV‚ÇÅ, hV‚ÇÇ, hV‚ÇÉ‚ü© := lebesgue_number_of_compact_open (hK.image f.continuous) hU hf
  refine' ‚ü®V, hV‚ÇÅ, hV‚ÇÇ, _‚ü©
  rintro g hg _ ‚ü®x, hx, rfl‚ü©
  exact hV‚ÇÉ (f x) ‚ü®x, hx, rfl‚ü© (hg x hx)
#align continuous_map.compact_conv_nhd_subset_compact_open ContinuousMap.compactConvNhd_subset_compactOpen

/-- The point `f` in `ContinuousMap.compactConvNhd K V f` is also an interior point wrt the
compact-open topology.

Since `ContinuousMap.compactConvNhd K V f` are a neighbourhood basis at `f` for each `f`, it follows
that the compact-open topology is at least as fine as the topology of compact convergence. -/
theorem iInter_compactOpen_gen_subset_compactConvNhd (hK : IsCompact K) (hV : V ‚àà ùì§ Œ≤) :
    ‚àÉ (Œπ : Sort (u‚ÇÅ + 1)) (_ : Fintype Œπ) (C : Œπ ‚Üí Set Œ±) (_hC : ‚àÄ i, IsCompact (C i))
      (U : Œπ ‚Üí Set Œ≤) (_hU : ‚àÄ i, IsOpen (U i)),
      (f ‚àà ‚ãÇ i, CompactOpen.gen (C i) (U i)) ‚àß
        ‚ãÇ i, CompactOpen.gen (C i) (U i) ‚äÜ compactConvNhd K V f := by
  obtain ‚ü®W, hW‚ÇÅ, hW‚ÇÑ, hW‚ÇÇ, hW‚ÇÉ‚ü© := comp_open_symm_mem_uniformity_sets hV
  obtain ‚ü®Z, hZ‚ÇÅ, hZ‚ÇÑ, hZ‚ÇÇ, hZ‚ÇÉ‚ü© := comp_open_symm_mem_uniformity_sets hW‚ÇÅ
  let U : Œ± ‚Üí Set Œ± := fun x => f ‚Åª¬π' ball (f x) Z
  have hU : ‚àÄ x, IsOpen (U x) := fun x => f.continuous.isOpen_preimage _ (isOpen_ball _ hZ‚ÇÑ)
  have hUK : K ‚äÜ ‚ãÉ x : K, U (x : K) := by
    intro x hx
    simp only [exists_prop, mem_iUnion, iUnion_coe_set, mem_preimage]
    exact ‚ü®(‚ü®x, hx‚ü© : K), by simp [hx, mem_ball_self (f x) hZ‚ÇÅ]‚ü©
  obtain ‚ü®t, ht‚ü© := hK.elim_finite_subcover _ (fun x : K => hU x.val) hUK
  let C : t ‚Üí Set Œ± := fun i => K ‚à© closure (U ((i : K) : Œ±))
  have hC : K ‚äÜ ‚ãÉ i, C i := by
    rw [‚Üê K.inter_iUnion, subset_inter_iff]
    refine' ‚ü®Subset.rfl, ht.trans _‚ü©
    simp only [SetCoe.forall, Subtype.coe_mk, iUnion_subset_iff]
    exact fun x hx‚ÇÅ hx‚ÇÇ => subset_iUnion_of_subset (‚ü®_, hx‚ÇÇ‚ü© : t) (by simp [subset_closure])
  have hfC : ‚àÄ i : t, C i ‚äÜ f ‚Åª¬π' ball (f ((i : K) : Œ±)) W := by
    simp only [‚Üê image_subset_iff, ‚Üê mem_preimage]
    rintro ‚ü®‚ü®x, hx‚ÇÅ‚ü©, hx‚ÇÇ‚ü©
    have hZW : closure (ball (f x) Z) ‚äÜ ball (f x) W := by
      intro y hy
      obtain ‚ü®z, hz‚ÇÅ, hz‚ÇÇ‚ü© := UniformSpace.mem_closure_iff_ball.mp hy hZ‚ÇÅ
      exact ball_mono hZ‚ÇÉ _ (mem_ball_comp hz‚ÇÇ ((mem_ball_symmetry hZ‚ÇÇ).mp hz‚ÇÅ))
    calc
      f '' (K ‚à© closure (U x)) ‚äÜ f '' closure (U x) := image_subset _ (inter_subset_right _ _)
      _ ‚äÜ closure (f '' U x) := f.continuous.continuousOn.image_closure
      _ ‚äÜ closure (ball (f x) Z) := by
        apply closure_mono
        simp only [image_subset_iff]
        rfl
      _ ‚äÜ ball (f x) W := hZW

  refine'
    ‚ü®t, t.fintypeCoeSort, C, fun i => hK.inter_right isClosed_closure, fun i =>
      ball (f ((i : K) : Œ±)) W, fun i => isOpen_ball _ hW‚ÇÑ, by simp [CompactOpen.gen, hfC],
      fun g hg x hx => hW‚ÇÉ (mem_compRel.mpr _)‚ü©
  simp only [mem_iInter, CompactOpen.gen, mem_setOf_eq, image_subset_iff] at hg
  obtain ‚ü®y, hy‚ü© := mem_iUnion.mp (hC hx)
  exact ‚ü®f y, (mem_ball_symmetry hW‚ÇÇ).mp (hfC y hy), mem_preimage.mp (hg y hy)‚ü©
#align continuous_map.Inter_compact_open_gen_subset_compact_conv_nhd ContinuousMap.iInter_compactOpen_gen_subset_compactConvNhd

/-- The compact-open topology is equal to the compact-convergence topology. -/
theorem compactOpen_eq_compactConvergence :
    ContinuousMap.compactOpen = (compactConvergenceTopology : TopologicalSpace C(Œ±, Œ≤)) := by
  rw [compactConvergenceTopology, ContinuousMap.compactOpen]
  refine' le_antisymm _ _
  ¬∑ refine' fun X hX => isOpen_iff_forall_mem_open.mpr fun f hf => _
    have hXf : X ‚àà (compactConvergenceFilterBasis f).filter := by
      rw [‚Üê nhds_compactConvergence]
      exact @IsOpen.mem_nhds C(Œ±, Œ≤) compactConvergenceTopology _ _ hX hf
    obtain ‚ü®-, ‚ü®‚ü®K, V‚ü©, ‚ü®hK, hV‚ü©, rfl‚ü©, hXf‚ü© := hXf
    obtain ‚ü®Œπ, hŒπ, C, hC, U, hU, h‚ÇÅ, h‚ÇÇ‚ü© := iInter_compactOpen_gen_subset_compactConvNhd f hK hV
    haveI := hŒπ
    exact
      ‚ü®‚ãÇ i, CompactOpen.gen (C i) (U i), h‚ÇÇ.trans hXf,
        isOpen_iInter fun i => ContinuousMap.isOpen_gen (hC i) (hU i), h‚ÇÅ‚ü©
  ¬∑ simp only [TopologicalSpace.le_generateFrom_iff_subset_isOpen, and_imp, exists_prop,
      forall_exists_index, setOf_subset_setOf]
    rintro - K hK U hU rfl f hf
    obtain ‚ü®V, hV, _hV', hVf‚ü© := compactConvNhd_subset_compactOpen f hK hU hf
    exact Filter.mem_of_superset (FilterBasis.mem_filter_of_mem _ ‚ü®‚ü®K, V‚ü©, ‚ü®hK, hV‚ü©, rfl‚ü©) hVf
#align continuous_map.compact_open_eq_compact_convergence ContinuousMap.compactOpen_eq_compactConvergence

/-- The filter on `C(Œ±, Œ≤) √ó C(Œ±, Œ≤)` which underlies the uniform space structure on `C(Œ±, Œ≤)`. -/
def compactConvergenceUniformity : Filter (C(Œ±, Œ≤) √ó C(Œ±, Œ≤)) :=
  ‚®Ö KV ‚àà { KV : Set Œ± √ó Set (Œ≤ √ó Œ≤) | IsCompact KV.1 ‚àß KV.2 ‚àà ùì§ Œ≤ },
    ùìü { fg : C(Œ±, Œ≤) √ó C(Œ±, Œ≤) | ‚àÄ x : Œ±, x ‚àà KV.1 ‚Üí (fg.1 x, fg.2 x) ‚àà KV.2 }
#align continuous_map.compact_convergence_uniformity ContinuousMap.compactConvergenceUniformity

theorem hasBasis_compactConvergenceUniformity_aux :
    HasBasis (@compactConvergenceUniformity Œ± Œ≤ _ _)
      (fun p : Set Œ± √ó Set (Œ≤ √ó Œ≤) => IsCompact p.1 ‚àß p.2 ‚àà ùì§ Œ≤) fun p =>
      { fg : C(Œ±, Œ≤) √ó C(Œ±, Œ≤) | ‚àÄ x ‚àà p.1, (fg.1 x, fg.2 x) ‚àà p.2 } := by
  refine' Filter.hasBasis_biInf_principal _ compactConvNhd_compact_entourage_nonempty
  rintro ‚ü®K‚ÇÅ, V‚ÇÅ‚ü© ‚ü®hK‚ÇÅ, hV‚ÇÅ‚ü© ‚ü®K‚ÇÇ, V‚ÇÇ‚ü© ‚ü®hK‚ÇÇ, hV‚ÇÇ‚ü©
  refine' ‚ü®‚ü®K‚ÇÅ ‚à™ K‚ÇÇ, V‚ÇÅ ‚à© V‚ÇÇ‚ü©, ‚ü®hK‚ÇÅ.union hK‚ÇÇ, Filter.inter_mem hV‚ÇÅ hV‚ÇÇ‚ü©, _‚ü©
  simp only [le_eq_subset, Prod.forall, setOf_subset_setOf, ge_iff_le, Order.Preimage, ‚Üê
    forall_and, mem_inter_iff, mem_union]
  exact fun f g => forall_imp fun x => by tauto
#align continuous_map.has_basis_compact_convergence_uniformity_aux ContinuousMap.hasBasis_compactConvergenceUniformity_aux

/-- An intermediate lemma. Usually `ContinuousMap.mem_compactConvergence_entourage_iff` is more
useful. -/
theorem mem_compactConvergenceUniformity (X : Set (C(Œ±, Œ≤) √ó C(Œ±, Œ≤))) :
    X ‚àà @compactConvergenceUniformity Œ± Œ≤ _ _ ‚Üî
      ‚àÉ (K : Set Œ±) (V : Set (Œ≤ √ó Œ≤)) (_hK : IsCompact K) (_hV : V ‚àà ùì§ Œ≤),
        { fg : C(Œ±, Œ≤) √ó C(Œ±, Œ≤) | ‚àÄ x ‚àà K, (fg.1 x, fg.2 x) ‚àà V } ‚äÜ X := by
  simp only [hasBasis_compactConvergenceUniformity_aux.mem_iff, exists_prop, Prod.exists,
    and_assoc]
#align continuous_map.mem_compact_convergence_uniformity ContinuousMap.mem_compactConvergenceUniformity

/-- Note that we ensure the induced topology is definitionally the compact-open topology. -/
instance compactConvergenceUniformSpace : UniformSpace C(Œ±, Œ≤)
    where
  uniformity := compactConvergenceUniformity
  refl := by
    simp only [compactConvergenceUniformity, and_imp, Filter.le_principal_iff, Prod.forall,
      Filter.mem_principal, mem_setOf_eq, le_iInf_iff, idRel_subset]
    exact fun K V _hK hV f x _hx => refl_mem_uniformity hV
  symm := by
    simp only [compactConvergenceUniformity, and_imp, Prod.forall, mem_setOf_eq, Prod.fst_swap,
      Filter.tendsto_principal, Prod.snd_swap, Filter.tendsto_iInf]
    intro K V hK hV
    obtain ‚ü®V', hV', hsymm, hsub‚ü© := symm_of_uniformity hV
    let X := { fg : C(Œ±, Œ≤) √ó C(Œ±, Œ≤) | ‚àÄ x : Œ±, x ‚àà K ‚Üí (fg.1 x, fg.2 x) ‚àà V' }
    have hX : X ‚àà compactConvergenceUniformity :=
      (mem_compactConvergenceUniformity X).mpr ‚ü®K, V', hK, hV', by simp‚ü©
    exact Filter.eventually_of_mem hX fun fg hfg x hx => hsub (hsymm _ _ (hfg x hx))
  comp X hX := by
    obtain ‚ü®K, V, hK, hV, hX‚ü© := (mem_compactConvergenceUniformity X).mp hX
    obtain ‚ü®V', hV', hcomp‚ü© := comp_mem_uniformity_sets hV
    let h := fun s : Set (C(Œ±, Œ≤) √ó C(Œ±, Œ≤)) => s ‚óã s
    suffices h { fg : C(Œ±, Œ≤) √ó C(Œ±, Œ≤) | ‚àÄ x ‚àà K, (fg.1 x, fg.2 x) ‚àà V' } ‚àà
        compactConvergenceUniformity.lift' h by
      apply Filter.mem_of_superset this
      rintro ‚ü®f, g‚ü© ‚ü®z, hz‚ÇÅ, hz‚ÇÇ‚ü©
      refine' hX fun x hx => hcomp _
      exact ‚ü®z x, hz‚ÇÅ x hx, hz‚ÇÇ x hx‚ü©
    apply Filter.mem_lift'
    exact (mem_compactConvergenceUniformity _).mpr ‚ü®K, V', hK, hV', Subset.refl _‚ü©
  isOpen_uniformity := by
    rw [compactOpen_eq_compactConvergence]
    refine' fun Y => forall‚ÇÇ_congr fun f hf => _
    simp only [mem_compactConvergence_nhd_filter, mem_compactConvergenceUniformity, Prod.forall,
      setOf_subset_setOf, compactConvNhd]
    refine' exists‚ÇÑ_congr fun K V _hK _hV => ‚ü®_, fun hY g hg => hY f g hg rfl‚ü©
    rintro hY g‚ÇÅ g‚ÇÇ hg‚ÇÅ rfl
    exact hY hg‚ÇÅ
#align continuous_map.compact_convergence_uniform_space ContinuousMap.compactConvergenceUniformSpace

theorem mem_compactConvergence_entourage_iff (X : Set (C(Œ±, Œ≤) √ó C(Œ±, Œ≤))) :
    X ‚àà ùì§ C(Œ±, Œ≤) ‚Üî
      ‚àÉ (K : Set Œ±) (V : Set (Œ≤ √ó Œ≤)) (_hK : IsCompact K) (_hV : V ‚àà ùì§ Œ≤),
        { fg : C(Œ±, Œ≤) √ó C(Œ±, Œ≤) | ‚àÄ x ‚àà K, (fg.1 x, fg.2 x) ‚àà V } ‚äÜ X :=
  mem_compactConvergenceUniformity X
#align continuous_map.mem_compact_convergence_entourage_iff ContinuousMap.mem_compactConvergence_entourage_iff

theorem hasBasis_compactConvergenceUniformity :
    HasBasis (ùì§ C(Œ±, Œ≤)) (fun p : Set Œ± √ó Set (Œ≤ √ó Œ≤) => IsCompact p.1 ‚àß p.2 ‚àà ùì§ Œ≤) fun p =>
      { fg : C(Œ±, Œ≤) √ó C(Œ±, Œ≤) | ‚àÄ x ‚àà p.1, (fg.1 x, fg.2 x) ‚àà p.2 } :=
  hasBasis_compactConvergenceUniformity_aux
#align continuous_map.has_basis_compact_convergence_uniformity ContinuousMap.hasBasis_compactConvergenceUniformity

theorem _root_.Filter.HasBasis.compactConvergenceUniformity {Œπ : Type*} {pi : Œπ ‚Üí Prop}
    {s : Œπ ‚Üí Set (Œ≤ √ó Œ≤)} (h : (ùì§ Œ≤).HasBasis pi s) :
    HasBasis (ùì§ C(Œ±, Œ≤)) (fun p : Set Œ± √ó Œπ => IsCompact p.1 ‚àß pi p.2) fun p =>
      { fg : C(Œ±, Œ≤) √ó C(Œ±, Œ≤) | ‚àÄ x ‚àà p.1, (fg.1 x, fg.2 x) ‚àà s p.2 } := by
  refine' hasBasis_compactConvergenceUniformity.to_hasBasis _ _
  ¬∑ rintro ‚ü®t‚ÇÅ, t‚ÇÇ‚ü© ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©
    rcases h.mem_iff.1 h‚ÇÇ with ‚ü®i, hpi, hi‚ü©
    exact ‚ü®(t‚ÇÅ, i), ‚ü®h‚ÇÅ, hpi‚ü©, fun fg hfg x hx => hi (hfg _ hx)‚ü©
  ¬∑ rintro ‚ü®t, i‚ü© ‚ü®ht, hi‚ü©
    exact ‚ü®(t, s i), ‚ü®ht, h.mem_of_mem hi‚ü©, Subset.rfl‚ü©
#align filter.has_basis.compact_convergence_uniformity Filter.HasBasis.compactConvergenceUniformity

variable {Œπ : Type u‚ÇÉ} {p : Filter Œπ} {F : Œπ ‚Üí C(Œ±, Œ≤)} {f}

theorem tendsto_iff_forall_compact_tendstoUniformlyOn :
    Tendsto F p (ùìù f) ‚Üî ‚àÄ K, IsCompact K ‚Üí TendstoUniformlyOn (fun i a => F i a) f p K := by
  rw [compactOpen_eq_compactConvergence, tendsto_iff_forall_compact_tendstoUniformlyOn']
#align continuous_map.tendsto_iff_forall_compact_tendsto_uniformly_on ContinuousMap.tendsto_iff_forall_compact_tendstoUniformlyOn

/-- Locally uniform convergence implies convergence in the compact-open topology. -/
theorem tendsto_of_tendstoLocallyUniformly (h : TendstoLocallyUniformly (fun i a => F i a) f p) :
    Tendsto F p (ùìù f) := by
  rw [tendsto_iff_forall_compact_tendstoUniformlyOn]
  intro K hK
  rw [‚Üê tendstoLocallyUniformlyOn_iff_tendstoUniformlyOn_of_compact hK]
  exact h.tendstoLocallyUniformlyOn
#align continuous_map.tendsto_of_tendsto_locally_uniformly ContinuousMap.tendsto_of_tendstoLocallyUniformly

/-- If every point has a compact neighbourhood, then convergence in the compact-open topology
implies locally uniform convergence.

See also `ContinuousMap.tendsto_iff_tendstoLocallyUniformly`, especially for T2 spaces. -/
theorem tendstoLocallyUniformly_of_tendsto (hŒ± : ‚àÄ x : Œ±, ‚àÉ n, IsCompact n ‚àß n ‚àà ùìù x)
    (h : Tendsto F p (ùìù f)) : TendstoLocallyUniformly (fun i a => F i a) f p := by
  rw [tendsto_iff_forall_compact_tendstoUniformlyOn] at h
  intro V hV x
  obtain ‚ü®n, hn‚ÇÅ, hn‚ÇÇ‚ü© := hŒ± x
  exact ‚ü®n, hn‚ÇÇ, h n hn‚ÇÅ V hV‚ü©
#align continuous_map.tendsto_locally_uniformly_of_tendsto ContinuousMap.tendstoLocallyUniformly_of_tendsto

/-- Convergence in the compact-open topology is the same as locally uniform convergence on a locally
compact space.

For non-T2 spaces, the assumption `LocallyCompactSpace Œ±` is stronger than we need and in fact
the `‚Üê` direction is true unconditionally. See `ContinuousMap.tendstoLocallyUniformly_of_tendsto`
and `ContinuousMap.tendsto_of_tendstoLocallyUniformly` for versions requiring weaker hypotheses. -/
theorem tendsto_iff_tendstoLocallyUniformly [LocallyCompactSpace Œ±] :
    Tendsto F p (ùìù f) ‚Üî TendstoLocallyUniformly (fun i a => F i a) f p :=
  ‚ü®tendstoLocallyUniformly_of_tendsto exists_compact_mem_nhds, tendsto_of_tendstoLocallyUniformly‚ü©
#align continuous_map.tendsto_iff_tendsto_locally_uniformly ContinuousMap.tendsto_iff_tendstoLocallyUniformly

section CompactDomain

variable [CompactSpace Œ±]

theorem hasBasis_compactConvergenceUniformity_of_compact :
    HasBasis (ùì§ C(Œ±, Œ≤)) (fun V : Set (Œ≤ √ó Œ≤) => V ‚àà ùì§ Œ≤) fun V =>
      { fg : C(Œ±, Œ≤) √ó C(Œ±, Œ≤) | ‚àÄ x, (fg.1 x, fg.2 x) ‚àà V } :=
  hasBasis_compactConvergenceUniformity.to_hasBasis
    (fun p hp => ‚ü®p.2, hp.2, fun _fg hfg x _hx => hfg x‚ü©) fun V hV =>
    ‚ü®‚ü®univ, V‚ü©, ‚ü®isCompact_univ, hV‚ü©, fun _fg hfg x => hfg x (mem_univ x)‚ü©
#align continuous_map.has_basis_compact_convergence_uniformity_of_compact ContinuousMap.hasBasis_compactConvergenceUniformity_of_compact

/-- Convergence in the compact-open topology is the same as uniform convergence for sequences of
continuous functions on a compact space. -/
theorem tendsto_iff_tendstoUniformly :
    Tendsto F p (ùìù f) ‚Üî TendstoUniformly (fun i a => F i a) f p := by
  rw [tendsto_iff_forall_compact_tendstoUniformlyOn, ‚Üê tendstoUniformlyOn_univ]
  exact ‚ü®fun h => h univ isCompact_univ, fun h K _hK => h.mono (subset_univ K)‚ü©
#align continuous_map.tendsto_iff_tendsto_uniformly ContinuousMap.tendsto_iff_tendstoUniformly

end CompactDomain

end ContinuousMap
