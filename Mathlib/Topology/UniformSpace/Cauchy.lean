/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro
-/
import Mathlib.Topology.Algebra.Constructions
import Mathlib.Topology.Bases
import Mathlib.Algebra.Order.Group.Nat
import Mathlib.Topology.UniformSpace.DiscreteUniformity

/-!
# Theory of Cauchy filters in uniform spaces. Complete uniform spaces. Totally bounded subsets.
-/

universe u v

open Filter Function TopologicalSpace Topology Set UniformSpace Uniformity

variable {Î± : Type u} {Î² : Type v} [uniformSpace : UniformSpace Î±]

/-- A filter `f` is Cauchy if for every entourage `r`, there exists an
  `s âˆˆ f` such that `s Ã— s âŠ† r`. This is a generalization of Cauchy
  sequences, because if `a : â„• â†’ Î±` then the filter of sets containing
  cofinitely many of the `a n` is Cauchy iff `a` is a Cauchy sequence. -/
def Cauchy (f : Filter Î±) :=
  NeBot f âˆ§ f Ã—Ë¢ f â‰¤ ğ“¤ Î±

/-- A set `s` is called *complete*, if any Cauchy filter `f` such that `s âˆˆ f`
has a limit in `s` (formally, it satisfies `f â‰¤ ğ“ x` for some `x âˆˆ s`). -/
def IsComplete (s : Set Î±) :=
  âˆ€ f, Cauchy f â†’ f â‰¤ ğ“Ÿ s â†’ âˆƒ x âˆˆ s, f â‰¤ ğ“ x

theorem Filter.HasBasis.cauchy_iff {Î¹} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set (Î± Ã— Î±)} (h : (ğ“¤ Î±).HasBasis p s)
    {f : Filter Î±} :
    Cauchy f â†” NeBot f âˆ§ âˆ€ i, p i â†’ âˆƒ t âˆˆ f, âˆ€ x âˆˆ t, âˆ€ y âˆˆ t, (x, y) âˆˆ s i :=
  and_congr Iff.rfl <|
    (f.basis_sets.prod_self.le_basis_iff h).trans <| by
      simp only [subset_def, Prod.forall, mem_prod_eq, and_imp, id, forall_mem_comm]

theorem cauchy_iff' {f : Filter Î±} :
    Cauchy f â†” NeBot f âˆ§ âˆ€ s âˆˆ ğ“¤ Î±, âˆƒ t âˆˆ f, âˆ€ x âˆˆ t, âˆ€ y âˆˆ t, (x, y) âˆˆ s :=
  (ğ“¤ Î±).basis_sets.cauchy_iff

theorem cauchy_iff {f : Filter Î±} : Cauchy f â†” NeBot f âˆ§ âˆ€ s âˆˆ ğ“¤ Î±, âˆƒ t âˆˆ f, t Ã—Ë¢ t âŠ† s :=
  cauchy_iff'.trans <| by
    simp only [subset_def, Prod.forall, mem_prod_eq, and_imp, forall_mem_comm]

lemma cauchy_iff_le {l : Filter Î±} [hl : l.NeBot] :
    Cauchy l â†” l Ã—Ë¢ l â‰¤ ğ“¤ Î± := by
  simp only [Cauchy, hl, true_and]

theorem Cauchy.ultrafilter_of {l : Filter Î±} (h : Cauchy l) :
    Cauchy (@Ultrafilter.of _ l h.1 : Filter Î±) := by
  haveI := h.1
  have := Ultrafilter.of_le l
  exact âŸ¨Ultrafilter.neBot _, (Filter.prod_mono this this).trans h.2âŸ©

theorem cauchy_map_iff {l : Filter Î²} {f : Î² â†’ Î±} :
    Cauchy (l.map f) â†” NeBot l âˆ§ Tendsto (fun p : Î² Ã— Î² => (f p.1, f p.2)) (l Ã—Ë¢ l) (ğ“¤ Î±) := by
  rw [Cauchy, map_neBot_iff, prod_map_map_eq, Tendsto]

theorem cauchy_map_iff' {l : Filter Î²} [hl : NeBot l] {f : Î² â†’ Î±} :
    Cauchy (l.map f) â†” Tendsto (fun p : Î² Ã— Î² => (f p.1, f p.2)) (l Ã—Ë¢ l) (ğ“¤ Î±) :=
  cauchy_map_iff.trans <| and_iff_right hl

theorem Cauchy.mono {f g : Filter Î±} [hg : NeBot g] (h_c : Cauchy f) (h_le : g â‰¤ f) : Cauchy g :=
  âŸ¨hg, le_trans (Filter.prod_mono h_le h_le) h_c.rightâŸ©

theorem Cauchy.mono' {f g : Filter Î±} (h_c : Cauchy f) (_ : NeBot g) (h_le : g â‰¤ f) : Cauchy g :=
  h_c.mono h_le

theorem cauchy_nhds {a : Î±} : Cauchy (ğ“ a) :=
  âŸ¨nhds_neBot, nhds_prod_eq.symm.trans_le (nhds_le_uniformity a)âŸ©

theorem cauchy_pure {a : Î±} : Cauchy (pure a) :=
  cauchy_nhds.mono (pure_le_nhds a)

theorem Filter.Tendsto.cauchy_map {l : Filter Î²} [NeBot l] {f : Î² â†’ Î±} {a : Î±}
    (h : Tendsto f l (ğ“ a)) : Cauchy (map f l) :=
  cauchy_nhds.mono h

lemma Cauchy.mono_uniformSpace {u v : UniformSpace Î²} {F : Filter Î²} (huv : u â‰¤ v)
    (hF : Cauchy (uniformSpace := u) F) : Cauchy (uniformSpace := v) F :=
  âŸ¨hF.1, hF.2.trans huvâŸ©

lemma cauchy_inf_uniformSpace {u v : UniformSpace Î²} {F : Filter Î²} :
    Cauchy (uniformSpace := u âŠ“ v) F â†”
    Cauchy (uniformSpace := u) F âˆ§ Cauchy (uniformSpace := v) F := by
  unfold Cauchy
  rw [inf_uniformity (u := u), le_inf_iff, and_and_left]

lemma cauchy_iInf_uniformSpace {Î¹ : Sort*} [Nonempty Î¹] {u : Î¹ â†’ UniformSpace Î²}
    {l : Filter Î²} :
    Cauchy (uniformSpace := â¨… i, u i) l â†” âˆ€ i, Cauchy (uniformSpace := u i) l := by
  unfold Cauchy
  rw [iInf_uniformity, le_iInf_iff, forall_and, forall_const]

lemma cauchy_iInf_uniformSpace' {Î¹ : Sort*} {u : Î¹ â†’ UniformSpace Î²}
    {l : Filter Î²} [l.NeBot] :
    Cauchy (uniformSpace := â¨… i, u i) l â†” âˆ€ i, Cauchy (uniformSpace := u i) l := by
  simp_rw [cauchy_iff_le (uniformSpace := _), iInf_uniformity, le_iInf_iff]

lemma cauchy_comap_uniformSpace {u : UniformSpace Î²} {Î±} {f : Î± â†’ Î²} {l : Filter Î±} :
    Cauchy (uniformSpace := comap f u) l â†” Cauchy (map f l) := by
  simp only [Cauchy, map_neBot_iff, prod_map_map_eq, map_le_iff_le_comap]
  rfl

lemma cauchy_prod_iff [UniformSpace Î²] {F : Filter (Î± Ã— Î²)} :
    Cauchy F â†” Cauchy (map Prod.fst F) âˆ§ Cauchy (map Prod.snd F) := by
  simp_rw [instUniformSpaceProd, â† cauchy_comap_uniformSpace, â† cauchy_inf_uniformSpace]

theorem Cauchy.prod [UniformSpace Î²] {f : Filter Î±} {g : Filter Î²} (hf : Cauchy f) (hg : Cauchy g) :
    Cauchy (f Ã—Ë¢ g) := by
  have := hf.1; have := hg.1
  simpa [cauchy_prod_iff, hf.1] using âŸ¨hf, hgâŸ©

/-- The common part of the proofs of `le_nhds_of_cauchy_adhp` and
`SequentiallyComplete.le_nhds_of_seq_tendsto_nhds`: if for any entourage `s`
one can choose a set `t âˆˆ f` of diameter `s` such that it contains a point `y`
with `(x, y) âˆˆ s`, then `f` converges to `x`. -/
theorem le_nhds_of_cauchy_adhp_aux {f : Filter Î±} {x : Î±}
    (adhs : âˆ€ s âˆˆ ğ“¤ Î±, âˆƒ t âˆˆ f, t Ã—Ë¢ t âŠ† s âˆ§ âˆƒ y, (x, y) âˆˆ s âˆ§ y âˆˆ t) : f â‰¤ ğ“ x := by
  -- Consider a neighborhood `s` of `x`
  intro s hs
  -- Take an entourage twice smaller than `s`
  rcases comp_mem_uniformity_sets (mem_nhds_uniformity_iff_right.1 hs) with âŸ¨U, U_mem, hUâŸ©
  -- Take a set `t âˆˆ f`, `t Ã— t âŠ† U`, and a point `y âˆˆ t` such that `(x, y) âˆˆ U`
  rcases adhs U U_mem with âŸ¨t, t_mem, ht, y, hxy, hyâŸ©
  apply mem_of_superset t_mem
  -- Given a point `z âˆˆ t`, we have `(x, y) âˆˆ U` and `(y, z) âˆˆ t Ã— t âŠ† U`, hence `z âˆˆ s`
  exact fun z hz => hU (prodMk_mem_compRel hxy (ht <| mk_mem_prod hy hz)) rfl

/-- If `x` is an adherent (cluster) point for a Cauchy filter `f`, then it is a limit point
for `f`. -/
theorem le_nhds_of_cauchy_adhp {f : Filter Î±} {x : Î±} (hf : Cauchy f) (adhs : ClusterPt x f) :
    f â‰¤ ğ“ x :=
  le_nhds_of_cauchy_adhp_aux
    (fun s hs => by
      obtain âŸ¨t, t_mem, htâŸ© : âˆƒ t âˆˆ f, t Ã—Ë¢ t âŠ† s := (cauchy_iff.1 hf).2 s hs
      use t, t_mem, ht
      exact forall_mem_nonempty_iff_neBot.2 adhs _ (inter_mem_inf (mem_nhds_left x hs) t_mem))

theorem le_nhds_iff_adhp_of_cauchy {f : Filter Î±} {x : Î±} (hf : Cauchy f) :
    f â‰¤ ğ“ x â†” ClusterPt x f :=
  âŸ¨fun h => ClusterPt.of_le_nhds' h hf.1, le_nhds_of_cauchy_adhp hfâŸ©

nonrec theorem Cauchy.map [UniformSpace Î²] {f : Filter Î±} {m : Î± â†’ Î²} (hf : Cauchy f)
    (hm : UniformContinuous m) : Cauchy (map m f) :=
  âŸ¨hf.1.map _,
    calc
      map m f Ã—Ë¢ map m f = map (Prod.map m m) (f Ã—Ë¢ f) := Filter.prod_map_map_eq
      _ â‰¤ Filter.map (Prod.map m m) (ğ“¤ Î±) := map_mono hf.right
      _ â‰¤ ğ“¤ Î² := hmâŸ©

nonrec theorem Cauchy.comap [UniformSpace Î²] {f : Filter Î²} {m : Î± â†’ Î²} (hf : Cauchy f)
    (hm : comap (fun p : Î± Ã— Î± => (m p.1, m p.2)) (ğ“¤ Î²) â‰¤ ğ“¤ Î±) [NeBot (comap m f)] :
    Cauchy (comap m f) :=
  âŸ¨â€¹_â€º,
    calc
      comap m f Ã—Ë¢ comap m f = comap (Prod.map m m) (f Ã—Ë¢ f) := prod_comap_comap_eq
      _ â‰¤ comap (Prod.map m m) (ğ“¤ Î²) := comap_mono hf.right
      _ â‰¤ ğ“¤ Î± := hmâŸ©

theorem Cauchy.comap' [UniformSpace Î²] {f : Filter Î²} {m : Î± â†’ Î²} (hf : Cauchy f)
    (hm : Filter.comap (fun p : Î± Ã— Î± => (m p.1, m p.2)) (ğ“¤ Î²) â‰¤ ğ“¤ Î±)
    (_ : NeBot (Filter.comap m f)) : Cauchy (Filter.comap m f) :=
  hf.comap hm

/-- Cauchy sequences. Usually defined on â„•, but often it is also useful to say that a function
defined on â„ is Cauchy at +âˆ to deduce convergence. Therefore, we define it in a type class that
is general enough to cover both â„• and â„, which are the main motivating examples. -/
def CauchySeq [Preorder Î²] (u : Î² â†’ Î±) :=
  Cauchy (atTop.map u)

theorem CauchySeq.tendsto_uniformity [Preorder Î²] {u : Î² â†’ Î±} (h : CauchySeq u) :
    Tendsto (Prod.map u u) atTop (ğ“¤ Î±) := by
  simpa only [Tendsto, prod_map_map_eq', prod_atTop_atTop_eq] using h.right

theorem CauchySeq.nonempty [Preorder Î²] {u : Î² â†’ Î±} (hu : CauchySeq u) : Nonempty Î² :=
  @nonempty_of_neBot _ _ <| (map_neBot_iff _).1 hu.1

theorem CauchySeq.mem_entourage {Î² : Type*} [SemilatticeSup Î²] {u : Î² â†’ Î±} (h : CauchySeq u)
    {V : Set (Î± Ã— Î±)} (hV : V âˆˆ ğ“¤ Î±) : âˆƒ kâ‚€, âˆ€ i j, kâ‚€ â‰¤ i â†’ kâ‚€ â‰¤ j â†’ (u i, u j) âˆˆ V := by
  haveI := h.nonempty
  have := h.tendsto_uniformity; rw [â† prod_atTop_atTop_eq] at this
  simpa [MapsTo] using atTop_basis.prod_self.tendsto_left_iff.1 this V hV

theorem Filter.Tendsto.cauchySeq [SemilatticeSup Î²] [Nonempty Î²] {f : Î² â†’ Î±} {x}
    (hx : Tendsto f atTop (ğ“ x)) : CauchySeq f :=
  hx.cauchy_map

theorem cauchySeq_const [SemilatticeSup Î²] [Nonempty Î²] (x : Î±) : CauchySeq fun _ : Î² => x :=
  tendsto_const_nhds.cauchySeq

theorem cauchySeq_iff_tendsto [Nonempty Î²] [SemilatticeSup Î²] {u : Î² â†’ Î±} :
    CauchySeq u â†” Tendsto (Prod.map u u) atTop (ğ“¤ Î±) :=
  cauchy_map_iff'.trans <| by simp only [prod_atTop_atTop_eq, Prod.map_def]

theorem CauchySeq.comp_tendsto {Î³} [Preorder Î²] [SemilatticeSup Î³] [Nonempty Î³] {f : Î² â†’ Î±}
    (hf : CauchySeq f) {g : Î³ â†’ Î²} (hg : Tendsto g atTop atTop) : CauchySeq (f âˆ˜ g) :=
  âŸ¨inferInstance, le_trans (prod_le_prod.mpr âŸ¨Tendsto.comp le_rfl hg, Tendsto.comp le_rfl hgâŸ©) hf.2âŸ©

theorem CauchySeq.comp_injective [SemilatticeSup Î²] [NoMaxOrder Î²] [Nonempty Î²] {u : â„• â†’ Î±}
    (hu : CauchySeq u) {f : Î² â†’ â„•} (hf : Injective f) : CauchySeq (u âˆ˜ f) :=
  hu.comp_tendsto <| Nat.cofinite_eq_atTop â–¸ hf.tendsto_cofinite.mono_left atTop_le_cofinite

theorem Function.Bijective.cauchySeq_comp_iff {f : â„• â†’ â„•} (hf : Bijective f) (u : â„• â†’ Î±) :
    CauchySeq (u âˆ˜ f) â†” CauchySeq u := by
  refine âŸ¨fun H => ?_, fun H => H.comp_injective hf.injectiveâŸ©
  lift f to â„• â‰ƒ â„• using hf
  simpa only [Function.comp_def, f.apply_symm_apply] using H.comp_injective f.symm.injective

theorem CauchySeq.subseq_subseq_mem {V : â„• â†’ Set (Î± Ã— Î±)} (hV : âˆ€ n, V n âˆˆ ğ“¤ Î±) {u : â„• â†’ Î±}
    (hu : CauchySeq u) {f g : â„• â†’ â„•} (hf : Tendsto f atTop atTop) (hg : Tendsto g atTop atTop) :
    âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ âˆ€ n, ((u âˆ˜ f âˆ˜ Ï†) n, (u âˆ˜ g âˆ˜ Ï†) n) âˆˆ V n := by
  rw [cauchySeq_iff_tendsto] at hu
  exact ((hu.comp <| hf.prod_atTop hg).comp tendsto_atTop_diagonal).subseq_mem hV

-- todo: generalize this and other lemmas to a nonempty semilattice
theorem cauchySeq_iff' {u : â„• â†’ Î±} :
    CauchySeq u â†” âˆ€ V âˆˆ ğ“¤ Î±, âˆ€á¶  k in atTop, k âˆˆ Prod.map u u â»Â¹' V :=
  cauchySeq_iff_tendsto

theorem cauchySeq_iff {u : â„• â†’ Î±} :
    CauchySeq u â†” âˆ€ V âˆˆ ğ“¤ Î±, âˆƒ N, âˆ€ k â‰¥ N, âˆ€ l â‰¥ N, (u k, u l) âˆˆ V := by
  simp only [cauchySeq_iff', Filter.eventually_atTop_prod_self', mem_preimage, Prod.map_apply]

theorem CauchySeq.prodMap {Î³ Î´} [UniformSpace Î²] [Preorder Î³] [Preorder Î´] {u : Î³ â†’ Î±} {v : Î´ â†’ Î²}
    (hu : CauchySeq u) (hv : CauchySeq v) : CauchySeq (Prod.map u v) := by
  simpa only [CauchySeq, prod_map_map_eq', prod_atTop_atTop_eq] using hu.prod hv

@[deprecated (since := "2025-03-10")]
alias CauchySeq.prod_map := CauchySeq.prodMap

theorem CauchySeq.prodMk {Î³} [UniformSpace Î²] [Preorder Î³] {u : Î³ â†’ Î±} {v : Î³ â†’ Î²}
    (hu : CauchySeq u) (hv : CauchySeq v) : CauchySeq fun x => (u x, v x) :=
  haveI := hu.1.of_map
  (Cauchy.prod hu hv).mono (tendsto_map.prodMk tendsto_map)

@[deprecated (since := "2025-03-10")]
alias CauchySeq.prod := CauchySeq.prodMk

theorem CauchySeq.eventually_eventually [Preorder Î²] {u : Î² â†’ Î±} (hu : CauchySeq u)
    {V : Set (Î± Ã— Î±)} (hV : V âˆˆ ğ“¤ Î±) : âˆ€á¶  k in atTop, âˆ€á¶  l in atTop, (u k, u l) âˆˆ V :=
  eventually_atTop_curry <| hu.tendsto_uniformity hV

theorem UniformContinuous.comp_cauchySeq {Î³} [UniformSpace Î²] [Preorder Î³] {f : Î± â†’ Î²}
    (hf : UniformContinuous f) {u : Î³ â†’ Î±} (hu : CauchySeq u) : CauchySeq (f âˆ˜ u) :=
  hu.map hf

theorem CauchySeq.subseq_mem {V : â„• â†’ Set (Î± Ã— Î±)} (hV : âˆ€ n, V n âˆˆ ğ“¤ Î±) {u : â„• â†’ Î±}
    (hu : CauchySeq u) : âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ âˆ€ n, (u <| Ï† (n + 1), u <| Ï† n) âˆˆ V n := by
  have : âˆ€ n, âˆƒ N, âˆ€ k â‰¥ N, âˆ€ l â‰¥ k, (u l, u k) âˆˆ V n := fun n => by
    rw [cauchySeq_iff] at hu
    rcases hu _ (hV n) with âŸ¨N, HâŸ©
    exact âŸ¨N, fun k hk l hl => H _ (le_trans hk hl) _ hkâŸ©
  obtain âŸ¨Ï† : â„• â†’ â„•, Ï†_extr : StrictMono Ï†, hÏ† : âˆ€ n, âˆ€ l â‰¥ Ï† n, (u l, u <| Ï† n) âˆˆ V nâŸ© :=
    extraction_forall_of_eventually' this
  exact âŸ¨Ï†, Ï†_extr, fun n => hÏ† _ _ (Ï†_extr <| Nat.lt_add_one n).leâŸ©

theorem Filter.Tendsto.subseq_mem_entourage {V : â„• â†’ Set (Î± Ã— Î±)} (hV : âˆ€ n, V n âˆˆ ğ“¤ Î±) {u : â„• â†’ Î±}
    {a : Î±} (hu : Tendsto u atTop (ğ“ a)) : âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ (u (Ï† 0), a) âˆˆ V 0 âˆ§
      âˆ€ n, (u <| Ï† (n + 1), u <| Ï† n) âˆˆ V (n + 1) := by
  rcases mem_atTop_sets.1 (hu (ball_mem_nhds a (symm_le_uniformity <| hV 0))) with âŸ¨n, hnâŸ©
  rcases (hu.comp (tendsto_add_atTop_nat n)).cauchySeq.subseq_mem fun n => hV (n + 1) with
    âŸ¨Ï†, Ï†_mono, hÏ†VâŸ©
  exact âŸ¨fun k => Ï† k + n, Ï†_mono.add_const _, hn _ le_add_self, hÏ†VâŸ©

/-- If a Cauchy sequence has a convergent subsequence, then it converges. -/
theorem tendsto_nhds_of_cauchySeq_of_subseq [Preorder Î²] {u : Î² â†’ Î±} (hu : CauchySeq u)
    {Î¹ : Type*} {f : Î¹ â†’ Î²} {p : Filter Î¹} [NeBot p] (hf : Tendsto f p atTop) {a : Î±}
    (ha : Tendsto (u âˆ˜ f) p (ğ“ a)) : Tendsto u atTop (ğ“ a) :=
  le_nhds_of_cauchy_adhp hu (ha.mapClusterPt.of_comp hf)

/-- Any shift of a Cauchy sequence is also a Cauchy sequence. -/
theorem cauchySeq_shift {u : â„• â†’ Î±} (k : â„•) : CauchySeq (fun n â†¦ u (n + k)) â†” CauchySeq u := by
  constructor <;> intro h
  Â· rw [cauchySeq_iff] at h âŠ¢
    intro V mV
    obtain âŸ¨N, hâŸ© := h V mV
    use N + k
    intro a ha b hb
    convert h (a - k) (Nat.le_sub_of_add_le ha) (b - k) (Nat.le_sub_of_add_le hb) <;> omega
  Â· exact h.comp_tendsto (tendsto_add_atTop_nat k)

theorem Filter.HasBasis.cauchySeq_iff {Î³} [Nonempty Î²] [SemilatticeSup Î²] {u : Î² â†’ Î±} {p : Î³ â†’ Prop}
    {s : Î³ â†’ Set (Î± Ã— Î±)} (h : (ğ“¤ Î±).HasBasis p s) :
    CauchySeq u â†” âˆ€ i, p i â†’ âˆƒ N, âˆ€ m, N â‰¤ m â†’ âˆ€ n, N â‰¤ n â†’ (u m, u n) âˆˆ s i := by
  rw [cauchySeq_iff_tendsto, â† prod_atTop_atTop_eq]
  refine (atTop_basis.prod_self.tendsto_iff h).trans ?_
  simp only [true_and, Prod.forall, mem_prod_eq,
    mem_Ici, and_imp, Prod.map, @forall_swap (_ â‰¤ _) Î²]

theorem Filter.HasBasis.cauchySeq_iff' {Î³} [Nonempty Î²] [SemilatticeSup Î²] {u : Î² â†’ Î±}
    {p : Î³ â†’ Prop} {s : Î³ â†’ Set (Î± Ã— Î±)} (H : (ğ“¤ Î±).HasBasis p s) :
    CauchySeq u â†” âˆ€ i, p i â†’ âˆƒ N, âˆ€ n â‰¥ N, (u n, u N) âˆˆ s i := by
  refine H.cauchySeq_iff.trans âŸ¨fun h i hi => ?_, fun h i hi => ?_âŸ©
  Â· exact (h i hi).imp fun N hN n hn => hN n hn N le_rfl
  Â· rcases comp_symm_of_uniformity (H.mem_of_mem hi) with âŸ¨t, ht, ht', htsâŸ©
    rcases H.mem_iff.1 ht with âŸ¨j, hj, hjtâŸ©
    refine (h j hj).imp fun N hN m hm n hn => hts âŸ¨u N, hjt ?_, ht' <| hjt ?_âŸ©
    exacts [hN m hm, hN n hn]

theorem cauchySeq_of_controlled [SemilatticeSup Î²] [Nonempty Î²] (U : Î² â†’ Set (Î± Ã— Î±))
    (hU : âˆ€ s âˆˆ ğ“¤ Î±, âˆƒ n, U n âŠ† s) {f : Î² â†’ Î±}
    (hf : âˆ€ â¦ƒN m n : Î²â¦„, N â‰¤ m â†’ N â‰¤ n â†’ (f m, f n) âˆˆ U N) : CauchySeq f :=
  cauchySeq_iff_tendsto.2
    (by
      intro s hs
      rw [mem_map, mem_atTop_sets]
      obtain âŸ¨N, hNâŸ© := hU s hs
      refine âŸ¨(N, N), fun mn hmn => ?_âŸ©
      obtain âŸ¨m, nâŸ© := mn
      exact hN (hf hmn.1 hmn.2))

theorem isComplete_iff_clusterPt {s : Set Î±} :
    IsComplete s â†” âˆ€ l, Cauchy l â†’ l â‰¤ ğ“Ÿ s â†’ âˆƒ x âˆˆ s, ClusterPt x l :=
  forallâ‚ƒ_congr fun _ hl _ => exists_congr fun _ => and_congr_right fun _ =>
    le_nhds_iff_adhp_of_cauchy hl

theorem isComplete_iff_ultrafilter {s : Set Î±} :
    IsComplete s â†” âˆ€ l : Ultrafilter Î±, Cauchy (l : Filter Î±) â†’ â†‘l â‰¤ ğ“Ÿ s â†’ âˆƒ x âˆˆ s, â†‘l â‰¤ ğ“ x := by
  refine âŸ¨fun h l => h l, fun H => isComplete_iff_clusterPt.2 fun l hl hls => ?_âŸ©
  haveI := hl.1
  rcases H (Ultrafilter.of l) hl.ultrafilter_of ((Ultrafilter.of_le l).trans hls) with âŸ¨x, hxs, hxlâŸ©
  exact âŸ¨x, hxs, (ClusterPt.of_le_nhds hxl).mono (Ultrafilter.of_le l)âŸ©

theorem isComplete_iff_ultrafilter' {s : Set Î±} :
    IsComplete s â†” âˆ€ l : Ultrafilter Î±, Cauchy (l : Filter Î±) â†’ s âˆˆ l â†’ âˆƒ x âˆˆ s, â†‘l â‰¤ ğ“ x :=
  isComplete_iff_ultrafilter.trans <| by simp only [le_principal_iff, Ultrafilter.mem_coe]

protected theorem IsComplete.union {s t : Set Î±} (hs : IsComplete s) (ht : IsComplete t) :
    IsComplete (s âˆª t) := by
  simp only [isComplete_iff_ultrafilter', Ultrafilter.union_mem_iff, or_imp] at *
  exact fun l hl =>
    âŸ¨fun hsl => (hs l hl hsl).imp fun x hx => âŸ¨Or.inl hx.1, hx.2âŸ©, fun htl =>
      (ht l hl htl).imp fun x hx => âŸ¨Or.inr hx.1, hx.2âŸ©âŸ©

theorem isComplete_iUnion_separated {Î¹ : Sort*} {s : Î¹ â†’ Set Î±} (hs : âˆ€ i, IsComplete (s i))
    {U : Set (Î± Ã— Î±)} (hU : U âˆˆ ğ“¤ Î±) (hd : âˆ€ (i j : Î¹), âˆ€ x âˆˆ s i, âˆ€ y âˆˆ s j, (x, y) âˆˆ U â†’ i = j) :
    IsComplete (â‹ƒ i, s i) := by
  set S := â‹ƒ i, s i
  intro l hl hls
  rw [le_principal_iff] at hls
  obtain âŸ¨hl_ne, hl'âŸ© := cauchy_iff.1 hl
  obtain âŸ¨t, htS, htl, htUâŸ© : âˆƒ t, t âŠ† S âˆ§ t âˆˆ l âˆ§ t Ã—Ë¢ t âŠ† U := by
    rcases hl' U hU with âŸ¨t, htl, htUâŸ©
    refine âŸ¨t âˆ© S, inter_subset_right, inter_mem htl hls, Subset.trans ?_ htUâŸ©
    gcongr <;> apply inter_subset_left
  obtain âŸ¨i, hiâŸ© : âˆƒ i, t âŠ† s i := by
    rcases Filter.nonempty_of_mem htl with âŸ¨x, hxâŸ©
    rcases mem_iUnion.1 (htS hx) with âŸ¨i, hiâŸ©
    refine âŸ¨i, fun y hy => ?_âŸ©
    rcases mem_iUnion.1 (htS hy) with âŸ¨j, hjâŸ©
    rwa [hd i j x hi y hj (htU <| mk_mem_prod hx hy)]
  rcases hs i l hl (le_principal_iff.2 <| mem_of_superset htl hi) with âŸ¨x, hxs, hlxâŸ©
  exact âŸ¨x, mem_iUnion.2 âŸ¨i, hxsâŸ©, hlxâŸ©

/-- A complete space is defined here using uniformities. A uniform space
  is complete if every Cauchy filter converges. -/
class CompleteSpace (Î± : Type u) [UniformSpace Î±] : Prop where
  /-- In a complete uniform space, every Cauchy filter converges. -/
  complete : âˆ€ {f : Filter Î±}, Cauchy f â†’ âˆƒ x, f â‰¤ ğ“ x

theorem complete_univ {Î± : Type u} [UniformSpace Î±] [CompleteSpace Î±] :
    IsComplete (univ : Set Î±) := fun f hf _ => by
  rcases CompleteSpace.complete hf with âŸ¨x, hxâŸ©
  exact âŸ¨x, mem_univ x, hxâŸ©

instance CompleteSpace.prod [UniformSpace Î²] [CompleteSpace Î±] [CompleteSpace Î²] :
    CompleteSpace (Î± Ã— Î²) where
  complete hf :=
    let âŸ¨x1, hx1âŸ© := CompleteSpace.complete <| hf.map uniformContinuous_fst
    let âŸ¨x2, hx2âŸ© := CompleteSpace.complete <| hf.map uniformContinuous_snd
    âŸ¨(x1, x2), by rw [nhds_prod_eq, le_prod]; constructor <;> assumptionâŸ©

lemma CompleteSpace.fst_of_prod [UniformSpace Î²] [CompleteSpace (Î± Ã— Î²)] [h : Nonempty Î²] :
    CompleteSpace Î± where
  complete hf :=
    let âŸ¨yâŸ© := h
    let âŸ¨(a, b), habâŸ© := CompleteSpace.complete <| hf.prod <| cauchy_pure (a := y)
    âŸ¨a, by simpa only [map_fst_prod, nhds_prod_eq] using map_mono (m := Prod.fst) habâŸ©

lemma CompleteSpace.snd_of_prod [UniformSpace Î²] [CompleteSpace (Î± Ã— Î²)] [h : Nonempty Î±] :
    CompleteSpace Î² where
  complete hf :=
    let âŸ¨xâŸ© := h
    let âŸ¨(a, b), habâŸ© := CompleteSpace.complete <| (cauchy_pure (a := x)).prod hf
    âŸ¨b, by simpa only [map_snd_prod, nhds_prod_eq] using map_mono (m := Prod.snd) habâŸ©

lemma completeSpace_prod_of_nonempty [UniformSpace Î²] [Nonempty Î±] [Nonempty Î²] :
    CompleteSpace (Î± Ã— Î²) â†” CompleteSpace Î± âˆ§ CompleteSpace Î² :=
  âŸ¨fun _ â†¦ âŸ¨.fst_of_prod (Î² := Î²), .snd_of_prod (Î± := Î±)âŸ©, fun âŸ¨_, _âŸ© â†¦ .prodâŸ©

@[to_additive]
instance CompleteSpace.mulOpposite [CompleteSpace Î±] : CompleteSpace Î±áµáµ’áµ– where
  complete hf :=
    MulOpposite.op_surjective.exists.mpr <|
      let âŸ¨x, hxâŸ© := CompleteSpace.complete (hf.map MulOpposite.uniformContinuous_unop)
      âŸ¨x, (map_le_iff_le_comap.mp hx).trans_eq <| MulOpposite.comap_unop_nhds _âŸ©

/-- If `univ` is complete, the space is a complete space -/
theorem completeSpace_of_isComplete_univ (h : IsComplete (univ : Set Î±)) : CompleteSpace Î± :=
  âŸ¨fun hf => let âŸ¨x, _, hxâŸ© := h _ hf ((@principal_univ Î±).symm â–¸ le_top); âŸ¨x, hxâŸ©âŸ©

theorem completeSpace_iff_isComplete_univ : CompleteSpace Î± â†” IsComplete (univ : Set Î±) :=
  âŸ¨@complete_univ Î± _, completeSpace_of_isComplete_univâŸ©

theorem completeSpace_iff_ultrafilter :
    CompleteSpace Î± â†” âˆ€ l : Ultrafilter Î±, Cauchy (l : Filter Î±) â†’ âˆƒ x : Î±, â†‘l â‰¤ ğ“ x := by
  simp [completeSpace_iff_isComplete_univ, isComplete_iff_ultrafilter]

theorem cauchy_iff_exists_le_nhds [CompleteSpace Î±] {l : Filter Î±} [NeBot l] :
    Cauchy l â†” âˆƒ x, l â‰¤ ğ“ x :=
  âŸ¨CompleteSpace.complete, fun âŸ¨_, hxâŸ© => cauchy_nhds.mono hxâŸ©

theorem cauchy_map_iff_exists_tendsto [CompleteSpace Î±] {l : Filter Î²} {f : Î² â†’ Î±} [NeBot l] :
    Cauchy (l.map f) â†” âˆƒ x, Tendsto f l (ğ“ x) :=
  cauchy_iff_exists_le_nhds

/-- A Cauchy sequence in a complete space converges -/
theorem cauchySeq_tendsto_of_complete [Preorder Î²] [CompleteSpace Î±] {u : Î² â†’ Î±}
    (H : CauchySeq u) : âˆƒ x, Tendsto u atTop (ğ“ x) :=
  CompleteSpace.complete H

/-- If `K` is a complete subset, then any cauchy sequence in `K` converges to a point in `K` -/
theorem cauchySeq_tendsto_of_isComplete [Preorder Î²] {K : Set Î±} (hâ‚ : IsComplete K)
    {u : Î² â†’ Î±} (hâ‚‚ : âˆ€ n, u n âˆˆ K) (hâ‚ƒ : CauchySeq u) : âˆƒ v âˆˆ K, Tendsto u atTop (ğ“ v) :=
  hâ‚ _ hâ‚ƒ <| le_principal_iff.2 <| mem_map_iff_exists_image.2
    âŸ¨univ, univ_mem, by rwa [image_univ, range_subset_iff]âŸ©

theorem Cauchy.le_nhds_lim [CompleteSpace Î±] {f : Filter Î±} (hf : Cauchy f) :
    haveI := hf.1.nonempty; f â‰¤ ğ“ (lim f) :=
  _root_.le_nhds_lim (CompleteSpace.complete hf)

theorem CauchySeq.tendsto_limUnder [Preorder Î²] [CompleteSpace Î±] {u : Î² â†’ Î±} (h : CauchySeq u) :
    haveI := h.1.nonempty; Tendsto u atTop (ğ“ <| limUnder atTop u) :=
  h.le_nhds_lim

theorem IsClosed.isComplete [CompleteSpace Î±] {s : Set Î±} (h : IsClosed s) : IsComplete s :=
  fun _ cf fs =>
  let âŸ¨x, hxâŸ© := CompleteSpace.complete cf
  âŸ¨x, isClosed_iff_clusterPt.mp h x (cf.left.mono (le_inf hx fs)), hxâŸ©

namespace DiscreteUniformity

variable [DiscreteUniformity Î±]

/-- A Cauchy filter in a discrete uniform space is contained in the principal filter
of a point. -/
theorem eq_pure_of_cauchy {f : Filter Î±} (hf : Cauchy f) : âˆƒ x : Î±, f = pure x := by
  rcases hf with âŸ¨f_ne_bot, f_leâŸ©
  simp only [DiscreteUniformity.eq_principal_idRel, le_principal_iff, mem_prod_iff] at f_le
  obtain âŸ¨S, âŸ¨hS, âŸ¨T, âŸ¨hT, HâŸ©âŸ©âŸ©âŸ© := f_le
  obtain âŸ¨x, rflâŸ© := eq_singleton_left_of_prod_subset_idRel (f_ne_bot.nonempty_of_mem hS)
    (Filter.nonempty_of_mem hT) H
  exact âŸ¨x, f_ne_bot.le_pure_iff.mp <| le_pure_iff.mpr hSâŸ©

@[deprecated (since := "2025-03-23")]
alias _root_.UniformSpace.DiscreteUnif.cauchy_le_pure := eq_pure_of_cauchy

/-- The discrete uniformity makes a space complete. -/
instance : CompleteSpace Î± where
  complete {f} hf := by
    obtain âŸ¨x, rflâŸ© := eq_pure_of_cauchy hf
    exact âŸ¨x, pure_le_nhds xâŸ©

variable {X}

/-- A constant to which a Cauchy filter in a discrete uniform space converges. -/
noncomputable def cauchyConst {f : Filter Î±} (hf : Cauchy f) : Î± :=
  (eq_pure_of_cauchy hf).choose

@[deprecated (since := "2025-03-23")]
alias _root_.UniformSpace.DiscreteUnif.cauchyConst := cauchyConst

theorem eq_pure_cauchyConst {f : Filter Î±} (hf : Cauchy f) : f = pure (cauchyConst hf) :=
  (eq_pure_of_cauchy hf).choose_spec

@[deprecated (since := "2025-03-23")]
alias _root_.UniformSpace.DiscreteUnif.eq_const_of_cauchy := eq_pure_cauchyConst

end DiscreteUniformity

/-- A set `s` is totally bounded if for every entourage `d` there is a finite
  set of points `t` such that every element of `s` is `d`-near to some element of `t`. -/
def TotallyBounded (s : Set Î±) : Prop :=
  âˆ€ d âˆˆ ğ“¤ Î±, âˆƒ t : Set Î±, t.Finite âˆ§ s âŠ† â‹ƒ y âˆˆ t, { x | (x, y) âˆˆ d }

theorem TotallyBounded.exists_subset {s : Set Î±} (hs : TotallyBounded s) {U : Set (Î± Ã— Î±)}
    (hU : U âˆˆ ğ“¤ Î±) : âˆƒ t, t âŠ† s âˆ§ Set.Finite t âˆ§ s âŠ† â‹ƒ y âˆˆ t, { x | (x, y) âˆˆ U } := by
  rcases comp_symm_of_uniformity hU with âŸ¨r, hr, rs, rUâŸ©
  rcases hs r hr with âŸ¨k, fk, ksâŸ©
  let u := k âˆ© { y | âˆƒ x âˆˆ s, (x, y) âˆˆ r }
  choose f hfs hfr using fun x : u => x.coe_prop.2
  refine âŸ¨range f, ?_, ?_, ?_âŸ©
  Â· exact range_subset_iff.2 hfs
  Â· haveI : Fintype u := (fk.inter_of_left _).fintype
    exact finite_range f
  Â· intro x xs
    obtain âŸ¨y, hy, xyâŸ© := mem_iUnionâ‚‚.1 (ks xs)
    rw [biUnion_range, mem_iUnion]
    set z : â†¥u := âŸ¨y, hy, âŸ¨x, xs, xyâŸ©âŸ©
    exact âŸ¨z, rU <| mem_compRel.2 âŸ¨y, xy, rs (hfr z)âŸ©âŸ©

theorem totallyBounded_iff_subset {s : Set Î±} :
    TotallyBounded s â†”
      âˆ€ d âˆˆ ğ“¤ Î±, âˆƒ t, t âŠ† s âˆ§ Set.Finite t âˆ§ s âŠ† â‹ƒ y âˆˆ t, { x | (x, y) âˆˆ d } :=
  âŸ¨fun H _ hd â†¦ H.exists_subset hd, fun H d hd â†¦ let âŸ¨t, _, htâŸ© := H d hd; âŸ¨t, htâŸ©âŸ©

theorem Filter.HasBasis.totallyBounded_iff {Î¹} {p : Î¹ â†’ Prop} {U : Î¹ â†’ Set (Î± Ã— Î±)}
    (H : (ğ“¤ Î±).HasBasis p U) {s : Set Î±} :
    TotallyBounded s â†” âˆ€ i, p i â†’ âˆƒ t : Set Î±, Set.Finite t âˆ§ s âŠ† â‹ƒ y âˆˆ t, { x | (x, y) âˆˆ U i } :=
  H.forall_iff fun _ _ hUV h =>
    h.imp fun _ ht => âŸ¨ht.1, ht.2.trans <| iUnionâ‚‚_mono fun _ _ _ hy => hUV hyâŸ©

theorem totallyBounded_of_forall_symm {s : Set Î±}
    (h : âˆ€ V âˆˆ ğ“¤ Î±, IsSymmetricRel V â†’ âˆƒ t : Set Î±, Set.Finite t âˆ§ s âŠ† â‹ƒ y âˆˆ t, ball y V) :
    TotallyBounded s :=
  UniformSpace.hasBasis_symmetric.totallyBounded_iff.2 fun V hV => by
    simpa only [ball_eq_of_symmetry hV.2] using h V hV.1 hV.2

theorem TotallyBounded.subset {sâ‚ sâ‚‚ : Set Î±} (hs : sâ‚ âŠ† sâ‚‚) (h : TotallyBounded sâ‚‚) :
    TotallyBounded sâ‚ := fun d hd =>
  let âŸ¨t, htâ‚, htâ‚‚âŸ© := h d hd
  âŸ¨t, htâ‚, Subset.trans hs htâ‚‚âŸ©

/-- The closure of a totally bounded set is totally bounded. -/
theorem TotallyBounded.closure {s : Set Î±} (h : TotallyBounded s) : TotallyBounded (closure s) :=
  uniformity_hasBasis_closed.totallyBounded_iff.2 fun V hV =>
    let âŸ¨t, htf, hstâŸ© := h V hV.1
    âŸ¨t, htf, closure_minimal hst <| htf.isClosed_biUnion fun _ _ => hV.2.preimage (.prodMk_left _)âŸ©

@[simp]
lemma totallyBounded_closure {s : Set Î±} : TotallyBounded (closure s) â†” TotallyBounded s :=
  âŸ¨fun h â†¦ h.subset subset_closure, TotallyBounded.closureâŸ©

/-- A finite indexed union is totally bounded
if and only if each set of the family is totally bounded. -/
@[simp]
lemma totallyBounded_iUnion {Î¹ : Sort*} [Finite Î¹] {s : Î¹ â†’ Set Î±} :
    TotallyBounded (â‹ƒ i, s i) â†” âˆ€ i, TotallyBounded (s i) := by
  refine âŸ¨fun h i â†¦ h.subset (subset_iUnion _ _), fun h U hU â†¦ ?_âŸ©
  choose t htf ht using (h Â· U hU)
  refine âŸ¨â‹ƒ i, t i, finite_iUnion htf, ?_âŸ©
  rw [biUnion_iUnion]
  gcongr; apply ht

/-- A union indexed by a finite set is totally bounded
if and only if each set of the family is totally bounded. -/
lemma totallyBounded_biUnion {Î¹ : Type*} {I : Set Î¹} (hI : I.Finite) {s : Î¹ â†’ Set Î±} :
    TotallyBounded (â‹ƒ i âˆˆ I, s i) â†” âˆ€ i âˆˆ I, TotallyBounded (s i) := by
  have := hI.to_subtype
  rw [biUnion_eq_iUnion, totallyBounded_iUnion, Subtype.forall]

/-- A union of a finite family of sets is totally bounded
if and only if each set of the family is totally bounded. -/
lemma totallyBounded_sUnion {S : Set (Set Î±)} (hS : S.Finite) :
    TotallyBounded (â‹ƒâ‚€ S) â†” âˆ€ s âˆˆ S, TotallyBounded s := by
  rw [sUnion_eq_biUnion, totallyBounded_biUnion hS]

/-- A finite set is totally bounded. -/
lemma Set.Finite.totallyBounded {s : Set Î±} (hs : s.Finite) : TotallyBounded s := fun _U hU â†¦
  âŸ¨s, hs, fun _x hx â†¦ mem_biUnion hx <| refl_mem_uniformity hUâŸ©

/-- A subsingleton is totally bounded. -/
lemma Set.Subsingleton.totallyBounded {s : Set Î±} (hs : s.Subsingleton) :
    TotallyBounded s :=
  hs.finite.totallyBounded

@[simp]
lemma totallyBounded_singleton (a : Î±) : TotallyBounded {a} := (finite_singleton a).totallyBounded

@[simp]
theorem totallyBounded_empty : TotallyBounded (âˆ… : Set Î±) := finite_empty.totallyBounded

/-- The union of two sets is totally bounded
if and only if each of the two sets is totally bounded. -/
@[simp]
lemma totallyBounded_union {s t : Set Î±} :
    TotallyBounded (s âˆª t) â†” TotallyBounded s âˆ§ TotallyBounded t := by
  rw [union_eq_iUnion, totallyBounded_iUnion]
  simp [and_comm]

/-- The union of two totally bounded sets is totally bounded. -/
protected lemma TotallyBounded.union {s t : Set Î±} (hs : TotallyBounded s) (ht : TotallyBounded t) :
    TotallyBounded (s âˆª t) :=
  totallyBounded_union.2 âŸ¨hs, htâŸ©

@[simp]
lemma totallyBounded_insert (a : Î±) {s : Set Î±} :
    TotallyBounded (insert a s) â†” TotallyBounded s := by
  simp_rw [â† singleton_union, totallyBounded_union, totallyBounded_singleton, true_and]

protected alias âŸ¨_, TotallyBounded.insertâŸ© := totallyBounded_insert

/-- The image of a totally bounded set under a uniformly continuous map is totally bounded. -/
theorem TotallyBounded.image [UniformSpace Î²] {f : Î± â†’ Î²} {s : Set Î±} (hs : TotallyBounded s)
    (hf : UniformContinuous f) : TotallyBounded (f '' s) := fun t ht =>
  have : { p : Î± Ã— Î± | (f p.1, f p.2) âˆˆ t } âˆˆ ğ“¤ Î± := hf ht
  let âŸ¨c, hfc, hctâŸ© := hs _ this
  âŸ¨f '' c, hfc.image f, by
    simp only [mem_image, iUnion_exists, biUnion_and', iUnion_iUnion_eq_right, image_subset_iff,
      preimage_iUnion, preimage_setOf_eq]
    simp? [subset_def] at hct says
      simp only [mem_setOf_eq, subset_def, mem_iUnion, exists_prop] at hct
    intro x hx
    simpa using hct x hxâŸ©

theorem Ultrafilter.cauchy_of_totallyBounded {s : Set Î±} (f : Ultrafilter Î±) (hs : TotallyBounded s)
    (h : â†‘f â‰¤ ğ“Ÿ s) : Cauchy (f : Filter Î±) :=
  âŸ¨f.neBot', fun _ ht =>
    let âŸ¨t', ht'â‚, ht'_symm, ht'_tâŸ© := comp_symm_of_uniformity ht
    let âŸ¨i, hi, hs_unionâŸ© := hs t' ht'â‚
    have : (â‹ƒ y âˆˆ i, { x | (x, y) âˆˆ t' }) âˆˆ f := mem_of_superset (le_principal_iff.mp h) hs_union
    have : âˆƒ y âˆˆ i, { x | (x, y) âˆˆ t' } âˆˆ f := (Ultrafilter.finite_biUnion_mem_iff hi).1 this
    let âŸ¨y, _, hifâŸ© := this
    have : { x | (x, y) âˆˆ t' } Ã—Ë¢ { x | (x, y) âˆˆ t' } âŠ† compRel t' t' :=
      fun âŸ¨_, _âŸ© âŸ¨(hâ‚ : (_, y) âˆˆ t'), (hâ‚‚ : (_, y) âˆˆ t')âŸ© => âŸ¨y, hâ‚, ht'_symm hâ‚‚âŸ©
    mem_of_superset (prod_mem_prod hif hif) (Subset.trans this ht'_t)âŸ©

theorem totallyBounded_iff_filter {s : Set Î±} :
    TotallyBounded s â†” âˆ€ f, NeBot f â†’ f â‰¤ ğ“Ÿ s â†’ âˆƒ c â‰¤ f, Cauchy c := by
  constructor
  Â· exact fun H f hf hfs => âŸ¨Ultrafilter.of f, Ultrafilter.of_le f,
      (Ultrafilter.of f).cauchy_of_totallyBounded H ((Ultrafilter.of_le f).trans hfs)âŸ©
  Â· intro H d hd
    contrapose! H with hd_cover
    set f := â¨… t : Finset Î±, ğ“Ÿ (s \ â‹ƒ y âˆˆ t, { x | (x, y) âˆˆ d })
    have hb : HasAntitoneBasis f fun t : Finset Î± â†¦ s \ â‹ƒ y âˆˆ t, { x | (x, y) âˆˆ d } :=
      .iInf_principal fun _ _ â†¦ diff_subset_diff_right âˆ˜ biUnion_subset_biUnion_left
    have : Filter.NeBot f := hb.1.neBot_iff.2 fun _ â†¦
      diff_nonempty.2 <| hd_cover _ (Finset.finite_toSet _)
    have : f â‰¤ ğ“Ÿ s := iInf_le_of_le âˆ… (by simp)
    refine âŸ¨f, â€¹_â€º, â€¹_â€º, fun c hcf hc => ?_âŸ©
    rcases mem_prod_same_iff.1 (hc.2 hd) with âŸ¨m, hm, hmdâŸ©
    rcases hc.1.nonempty_of_mem hm with âŸ¨y, hymâŸ©
    have : s \ {x | (x, y) âˆˆ d} âˆˆ c := by simpa using hcf (hb.mem {y})
    rcases hc.1.nonempty_of_mem (inter_mem hm this) with âŸ¨z, hzm, -, hyzâŸ©
    exact hyz (hmd âŸ¨hzm, hymâŸ©)

theorem totallyBounded_iff_ultrafilter {s : Set Î±} :
    TotallyBounded s â†” âˆ€ f : Ultrafilter Î±, â†‘f â‰¤ ğ“Ÿ s â†’ Cauchy (f : Filter Î±) := by
  refine âŸ¨fun hs f => f.cauchy_of_totallyBounded hs, fun H => totallyBounded_iff_filter.2 ?_âŸ©
  intro f hf hfs
  exact âŸ¨Ultrafilter.of f, Ultrafilter.of_le f, H _ ((Ultrafilter.of_le f).trans hfs)âŸ©

theorem isCompact_iff_totallyBounded_isComplete {s : Set Î±} :
    IsCompact s â†” TotallyBounded s âˆ§ IsComplete s :=
  âŸ¨fun hs =>
    âŸ¨totallyBounded_iff_ultrafilter.2 fun f hf =>
        let âŸ¨_, _, fxâŸ© := isCompact_iff_ultrafilter_le_nhds.1 hs f hf
        cauchy_nhds.mono fx,
      fun f fc fs =>
      let âŸ¨a, as, faâŸ© := @hs f fc.1 fs
      âŸ¨a, as, le_nhds_of_cauchy_adhp fc faâŸ©âŸ©,
    fun âŸ¨ht, hcâŸ© =>
    isCompact_iff_ultrafilter_le_nhds.2 fun f hf =>
      hc _ (totallyBounded_iff_ultrafilter.1 ht f hf) hfâŸ©

protected theorem IsCompact.totallyBounded {s : Set Î±} (h : IsCompact s) : TotallyBounded s :=
  (isCompact_iff_totallyBounded_isComplete.1 h).1

protected theorem IsCompact.isComplete {s : Set Î±} (h : IsCompact s) : IsComplete s :=
  (isCompact_iff_totallyBounded_isComplete.1 h).2

-- see Note [lower instance priority]
instance (priority := 100) complete_of_compact {Î± : Type u} [UniformSpace Î±] [CompactSpace Î±] :
    CompleteSpace Î± :=
  âŸ¨fun hf => by simpa using (isCompact_iff_totallyBounded_isComplete.1 isCompact_univ).2 _ hfâŸ©

theorem isCompact_of_totallyBounded_isClosed [CompleteSpace Î±] {s : Set Î±} (ht : TotallyBounded s)
    (hc : IsClosed s) : IsCompact s :=
  (@isCompact_iff_totallyBounded_isComplete Î± _ s).2 âŸ¨ht, hc.isCompleteâŸ©

/-- Every Cauchy sequence over `â„•` is totally bounded. -/
theorem CauchySeq.totallyBounded_range {s : â„• â†’ Î±} (hs : CauchySeq s) :
    TotallyBounded (range s) := by
  intro a ha
  obtain âŸ¨n, hnâŸ© := cauchySeq_iff.1 hs a ha
  refine âŸ¨s '' { k | k â‰¤ n }, (finite_le_nat _).image _, ?_âŸ©
  rw [range_subset_iff, biUnion_image]
  intro m
  rw [mem_iUnionâ‚‚]
  rcases le_total m n with hm | hm
  exacts [âŸ¨m, hm, refl_mem_uniformity haâŸ©, âŸ¨n, le_refl n, hn m hm n le_rflâŸ©]

/-- Given a family of points `xs n`, a family of entourages `V n` of the diagonal and a family of
natural numbers `u n`, the intersection over `n` of the `V n`-neighborhood of `xs 1, ..., xs (u n)`.
Designed to be relatively compact when `V n` tends to the diagonal. -/
def interUnionBalls (xs : â„• â†’ Î±) (u : â„• â†’ â„•) (V : â„• â†’ Set (Î± Ã— Î±)) : Set Î± :=
  â‹‚ n, â‹ƒ m â‰¤ u n, UniformSpace.ball (xs m) (Prod.swap â»Â¹' V n)

lemma totallyBounded_interUnionBalls {p : â„• â†’ Prop} {U : â„• â†’ Set (Î± Ã— Î±)}
    (H : (uniformity Î±).HasBasis p U) (xs : â„• â†’ Î±) (u : â„• â†’ â„•) :
    TotallyBounded (interUnionBalls xs u U) := by
  rw [Filter.HasBasis.totallyBounded_iff H]
  intro i _
  have h_subset : interUnionBalls xs u U
      âŠ† â‹ƒ m â‰¤ u i, UniformSpace.ball (xs m) (Prod.swap â»Â¹' U i) :=
    fun x hx â†¦ Set.mem_iInter.1 hx i
  classical
  refine âŸ¨Finset.image xs (Finset.range (u i + 1)), Finset.finite_toSet _, fun x hx â†¦ ?_âŸ©
  simp only [Finset.coe_image, Finset.coe_range, mem_image, mem_Iio, iUnion_exists, biUnion_and',
    iUnion_iUnion_eq_right, Nat.lt_succ_iff]
  exact h_subset hx

/-- The construction `interUnionBalls` is used to have a relatively compact set. -/
theorem isCompact_closure_interUnionBalls {p : â„• â†’ Prop} {U : â„• â†’ Set (Î± Ã— Î±)}
    (H : (uniformity Î±).HasBasis p U) [CompleteSpace Î±] (xs : â„• â†’ Î±) (u : â„• â†’ â„•) :
    IsCompact (closure (interUnionBalls xs u U)) := by
  rw [isCompact_iff_totallyBounded_isComplete]
  refine âŸ¨TotallyBounded.closure ?_, isClosed_closure.isCompleteâŸ©
  exact totallyBounded_interUnionBalls H xs u

/-!
### Sequentially complete space

In this section we prove that a uniform space is complete provided that it is sequentially complete
(i.e., any Cauchy sequence converges) and its uniformity filter admits a countable generating set.
In particular, this applies to (e)metric spaces, see the files `Topology/MetricSpace/EmetricSpace`
and `Topology/MetricSpace/Basic`.

More precisely, we assume that there is a sequence of entourages `U_n` such that any other
entourage includes one of `U_n`. Then any Cauchy filter `f` generates a decreasing sequence of
sets `s_n âˆˆ f` such that `s_n Ã— s_n âŠ† U_n`. Choose a sequence `x_nâˆˆs_n`. It is easy to show
that this is a Cauchy sequence. If this sequence converges to some `a`, then `f â‰¤ ğ“ a`. -/


namespace SequentiallyComplete

variable {f : Filter Î±} (hf : Cauchy f) {U : â„• â†’ Set (Î± Ã— Î±)} (U_mem : âˆ€ n, U n âˆˆ ğ“¤ Î±)

open Set Finset

noncomputable section

/-- An auxiliary sequence of sets approximating a Cauchy filter. -/
def setSeqAux (n : â„•) : { s : Set Î± // s âˆˆ f âˆ§ s Ã—Ë¢ s âŠ† U n } :=
  Classical.indefiniteDescription _ <| (cauchy_iff.1 hf).2 (U n) (U_mem n)

/-- Given a Cauchy filter `f` and a sequence `U` of entourages, `set_seq` provides
an antitone sequence of sets `s n âˆˆ f` such that `s n Ã—Ë¢ s n âŠ† U`. -/
def setSeq (n : â„•) : Set Î± :=
  â‹‚ m âˆˆ Set.Iic n, (setSeqAux hf U_mem m).val

theorem setSeq_mem (n : â„•) : setSeq hf U_mem n âˆˆ f :=
  (biInter_mem (finite_le_nat n)).2 fun m _ => (setSeqAux hf U_mem m).2.1

theorem setSeq_mono â¦ƒm n : â„•â¦„ (h : m â‰¤ n) : setSeq hf U_mem n âŠ† setSeq hf U_mem m :=
  biInter_subset_biInter_left <| Iic_subset_Iic.2 h

theorem setSeq_sub_aux (n : â„•) : setSeq hf U_mem n âŠ† setSeqAux hf U_mem n :=
  biInter_subset_of_mem right_mem_Iic

theorem setSeq_prod_subset {N m n} (hm : N â‰¤ m) (hn : N â‰¤ n) :
    setSeq hf U_mem m Ã—Ë¢ setSeq hf U_mem n âŠ† U N := fun p hp => by
  refine (setSeqAux hf U_mem N).2.2 âŸ¨?_, ?_âŸ© <;> apply setSeq_sub_aux
  Â· exact setSeq_mono hf U_mem hm hp.1
  Â· exact setSeq_mono hf U_mem hn hp.2

/-- A sequence of points such that `seq n âˆˆ setSeq n`. Here `setSeq` is an antitone
sequence of sets `setSeq n âˆˆ f` with diameters controlled by a given sequence
of entourages. -/
def seq (n : â„•) : Î± :=
  (hf.1.nonempty_of_mem (setSeq_mem hf U_mem n)).choose

theorem seq_mem (n : â„•) : seq hf U_mem n âˆˆ setSeq hf U_mem n :=
  (hf.1.nonempty_of_mem (setSeq_mem hf U_mem n)).choose_spec

theorem seq_pair_mem â¦ƒN m n : â„•â¦„ (hm : N â‰¤ m) (hn : N â‰¤ n) :
    (seq hf U_mem m, seq hf U_mem n) âˆˆ U N :=
  setSeq_prod_subset hf U_mem hm hn âŸ¨seq_mem hf U_mem m, seq_mem hf U_mem nâŸ©

theorem seq_is_cauchySeq (U_le : âˆ€ s âˆˆ ğ“¤ Î±, âˆƒ n, U n âŠ† s) : CauchySeq <| seq hf U_mem :=
  cauchySeq_of_controlled U U_le <| seq_pair_mem hf U_mem

/-- If the sequence `SequentiallyComplete.seq` converges to `a`, then `f â‰¤ ğ“ a`. -/
theorem le_nhds_of_seq_tendsto_nhds (U_le : âˆ€ s âˆˆ ğ“¤ Î±, âˆƒ n, U n âŠ† s)
    â¦ƒa : Î±â¦„ (ha : Tendsto (seq hf U_mem) atTop (ğ“ a)) : f â‰¤ ğ“ a :=
  le_nhds_of_cauchy_adhp_aux
    (fun s hs => by
      rcases U_le s hs with âŸ¨m, hmâŸ©
      rcases tendsto_atTop'.1 ha _ (mem_nhds_left a (U_mem m)) with âŸ¨n, hnâŸ©
      refine
        âŸ¨setSeq hf U_mem (max m n), setSeq_mem hf U_mem _, ?_, seq hf U_mem (max m n), ?_,
          seq_mem hf U_mem _âŸ©
      Â· have := le_max_left m n
        exact Set.Subset.trans (setSeq_prod_subset hf U_mem this this) hm
      Â· exact hm (hn _ <| le_max_right m n))

end

end SequentiallyComplete

namespace UniformSpace

open SequentiallyComplete

variable [IsCountablyGenerated (ğ“¤ Î±)]

/-- A uniform space is complete provided that (a) its uniformity filter has a countable basis;
(b) any sequence satisfying a "controlled" version of the Cauchy condition converges. -/
theorem complete_of_convergent_controlled_sequences (U : â„• â†’ Set (Î± Ã— Î±)) (U_mem : âˆ€ n, U n âˆˆ ğ“¤ Î±)
    (HU : âˆ€ u : â„• â†’ Î±, (âˆ€ N m n, N â‰¤ m â†’ N â‰¤ n â†’ (u m, u n) âˆˆ U N) â†’ âˆƒ a, Tendsto u atTop (ğ“ a)) :
    CompleteSpace Î± := by
  obtain âŸ¨U', -, hU'âŸ© := (ğ“¤ Î±).exists_antitone_seq
  have Hmem : âˆ€ n, U n âˆ© U' n âˆˆ ğ“¤ Î± := fun n => inter_mem (U_mem n) (hU'.2 âŸ¨n, Subset.refl _âŸ©)
  refine âŸ¨fun hf => (HU (seq hf Hmem) fun N m n hm hn => ?_).imp <|
    le_nhds_of_seq_tendsto_nhds _ _ fun s hs => ?_âŸ©
  Â· exact inter_subset_left (seq_pair_mem hf Hmem hm hn)
  Â· rcases hU'.1 hs with âŸ¨N, hNâŸ©
    exact âŸ¨N, Subset.trans inter_subset_right hNâŸ©

/-- A sequentially complete uniform space with a countable basis of the uniformity filter is
complete. -/
theorem complete_of_cauchySeq_tendsto (H' : âˆ€ u : â„• â†’ Î±, CauchySeq u â†’ âˆƒ a, Tendsto u atTop (ğ“ a)) :
    CompleteSpace Î± :=
  let âŸ¨U', _, hU'âŸ© := (ğ“¤ Î±).exists_antitone_seq
  complete_of_convergent_controlled_sequences U' (fun n => hU'.2 âŸ¨n, Subset.refl _âŸ©) fun u hu =>
    H' u <| cauchySeq_of_controlled U' (fun _ hs => hU'.1 hs) hu

variable (Î±)

-- TODO: move to `Topology.UniformSpace.Basic`
instance (priority := 100) firstCountableTopology : FirstCountableTopology Î± :=
  âŸ¨fun a => by rw [nhds_eq_comap_uniformity]; infer_instanceâŸ©

/-- A separable uniform space with countably generated uniformity filter is second countable:
one obtains a countable basis by taking the balls centered at points in a dense subset,
and with rational "radii" from a countable open symmetric antitone basis of `ğ“¤ Î±`. We do not
register this as an instance, as there is already an instance going in the other direction
from second countable spaces to separable spaces, and we want to avoid loops. -/
theorem secondCountable_of_separable [SeparableSpace Î±] : SecondCountableTopology Î± := by
  rcases exists_countable_dense Î± with âŸ¨s, hsc, hsdâŸ©
  obtain
    âŸ¨t : â„• â†’ Set (Î± Ã— Î±), hto : âˆ€ i : â„•, t i âˆˆ (ğ“¤ Î±).sets âˆ§ IsOpen (t i) âˆ§ IsSymmetricRel (t i),
      h_basis : (ğ“¤ Î±).HasAntitoneBasis tâŸ© :=
    (@uniformity_hasBasis_open_symmetric Î± _).exists_antitone_subbasis
  choose ht_mem hto hts using hto
  refine âŸ¨âŸ¨â‹ƒ x âˆˆ s, range fun k => ball x (t k), hsc.biUnion fun x _ => countable_range _, ?_âŸ©âŸ©
  refine (isTopologicalBasis_of_isOpen_of_nhds ?_ ?_).eq_generateFrom
  Â· simp only [mem_iUnionâ‚‚, mem_range]
    rintro _ âŸ¨x, _, k, rflâŸ©
    exact isOpen_ball x (hto k)
  Â· intro x V hxV hVo
    simp only [mem_iUnionâ‚‚, mem_range, exists_prop]
    rcases UniformSpace.mem_nhds_iff.1 (IsOpen.mem_nhds hVo hxV) with âŸ¨U, hU, hUVâŸ©
    rcases comp_symm_of_uniformity hU with âŸ¨U', hU', _, hUU'âŸ©
    rcases h_basis.toHasBasis.mem_iff.1 hU' with âŸ¨k, -, hkâŸ©
    rcases hsd.inter_open_nonempty (ball x <| t k) (isOpen_ball x (hto k))
        âŸ¨x, UniformSpace.mem_ball_self _ (ht_mem k)âŸ© with
      âŸ¨y, hxy, hysâŸ©
    refine âŸ¨_, âŸ¨y, hys, k, rflâŸ©, (hts k).subset hxy, fun z hz => ?_âŸ©
    exact hUV (ball_subset_of_comp_subset (hk hxy) hUU' (hk hz))

end UniformSpace
