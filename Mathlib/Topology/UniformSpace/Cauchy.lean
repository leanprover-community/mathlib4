/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro
-/
import Mathlib.Topology.Algebra.Constructions
import Mathlib.Topology.Bases
import Mathlib.Topology.UniformSpace.Basic

#align_import topology.uniform_space.cauchy from "leanprover-community/mathlib"@"22131150f88a2d125713ffa0f4693e3355b1eb49"

/-!
# Theory of Cauchy filters in uniform spaces. Complete uniform spaces. Totally bounded subsets.
-/


universe u v

open scoped Classical
open Filter TopologicalSpace Set UniformSpace Function

open scoped Classical
open Uniformity Topology Filter

variable {Î± : Type u} {Î² : Type v} [uniformSpace : UniformSpace Î±]

/-- A filter `f` is Cauchy if for every entourage `r`, there exists an
  `s âˆˆ f` such that `s Ã— s âŠ† r`. This is a generalization of Cauchy
  sequences, because if `a : â„• â†’ Î±` then the filter of sets containing
  cofinitely many of the `a n` is Cauchy iff `a` is a Cauchy sequence. -/
def Cauchy (f : Filter Î±) :=
  NeBot f âˆ§ f Ã—Ë¢ f â‰¤ ğ“¤ Î±
#align cauchy Cauchy

/-- A set `s` is called *complete*, if any Cauchy filter `f` such that `s âˆˆ f`
has a limit in `s` (formally, it satisfies `f â‰¤ ğ“ x` for some `x âˆˆ s`). -/
def IsComplete (s : Set Î±) :=
  âˆ€ f, Cauchy f â†’ f â‰¤ ğ“Ÿ s â†’ âˆƒ x âˆˆ s, f â‰¤ ğ“ x
#align is_complete IsComplete

theorem Filter.HasBasis.cauchy_iff {Î¹} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set (Î± Ã— Î±)} (h : (ğ“¤ Î±).HasBasis p s)
    {f : Filter Î±} :
    Cauchy f â†” NeBot f âˆ§ âˆ€ i, p i â†’ âˆƒ t âˆˆ f, âˆ€ x âˆˆ t, âˆ€ y âˆˆ t, (x, y) âˆˆ s i :=
  and_congr Iff.rfl <|
    (f.basis_sets.prod_self.le_basis_iff h).trans <| by
      simp only [subset_def, Prod.forall, mem_prod_eq, and_imp, id, forall_mem_comm]
#align filter.has_basis.cauchy_iff Filter.HasBasis.cauchy_iff

theorem cauchy_iff' {f : Filter Î±} :
    Cauchy f â†” NeBot f âˆ§ âˆ€ s âˆˆ ğ“¤ Î±, âˆƒ t âˆˆ f, âˆ€ x âˆˆ t, âˆ€ y âˆˆ t, (x, y) âˆˆ s :=
  (ğ“¤ Î±).basis_sets.cauchy_iff
#align cauchy_iff' cauchy_iff'

theorem cauchy_iff {f : Filter Î±} : Cauchy f â†” NeBot f âˆ§ âˆ€ s âˆˆ ğ“¤ Î±, âˆƒ t âˆˆ f, t Ã—Ë¢ t âŠ† s :=
  cauchy_iff'.trans <| by
    simp only [subset_def, Prod.forall, mem_prod_eq, and_imp, id, forall_mem_comm]
#align cauchy_iff cauchy_iff

lemma cauchy_iff_le {l : Filter Î±} [hl : l.NeBot] :
    Cauchy l â†” l Ã—Ë¢ l â‰¤ ğ“¤ Î± := by
  simp only [Cauchy, hl, true_and]

theorem Cauchy.ultrafilter_of {l : Filter Î±} (h : Cauchy l) :
    Cauchy (@Ultrafilter.of _ l h.1 : Filter Î±) := by
  haveI := h.1
  have := Ultrafilter.of_le l
  exact âŸ¨Ultrafilter.neBot _, (Filter.prod_mono this this).trans h.2âŸ©
#align cauchy.ultrafilter_of Cauchy.ultrafilter_of

theorem cauchy_map_iff {l : Filter Î²} {f : Î² â†’ Î±} :
    Cauchy (l.map f) â†” NeBot l âˆ§ Tendsto (fun p : Î² Ã— Î² => (f p.1, f p.2)) (l Ã—Ë¢ l) (ğ“¤ Î±) := by
  rw [Cauchy, map_neBot_iff, prod_map_map_eq, Tendsto]
#align cauchy_map_iff cauchy_map_iff

theorem cauchy_map_iff' {l : Filter Î²} [hl : NeBot l] {f : Î² â†’ Î±} :
    Cauchy (l.map f) â†” Tendsto (fun p : Î² Ã— Î² => (f p.1, f p.2)) (l Ã—Ë¢ l) (ğ“¤ Î±) :=
  cauchy_map_iff.trans <| and_iff_right hl
#align cauchy_map_iff' cauchy_map_iff'

theorem Cauchy.mono {f g : Filter Î±} [hg : NeBot g] (h_c : Cauchy f) (h_le : g â‰¤ f) : Cauchy g :=
  âŸ¨hg, le_trans (Filter.prod_mono h_le h_le) h_c.rightâŸ©
#align cauchy.mono Cauchy.mono

theorem Cauchy.mono' {f g : Filter Î±} (h_c : Cauchy f) (_ : NeBot g) (h_le : g â‰¤ f) : Cauchy g :=
  h_c.mono h_le
#align cauchy.mono' Cauchy.mono'

theorem cauchy_nhds {a : Î±} : Cauchy (ğ“ a) :=
  âŸ¨nhds_neBot, nhds_prod_eq.symm.trans_le (nhds_le_uniformity a)âŸ©
#align cauchy_nhds cauchy_nhds

theorem cauchy_pure {a : Î±} : Cauchy (pure a) :=
  cauchy_nhds.mono (pure_le_nhds a)
#align cauchy_pure cauchy_pure

theorem Filter.Tendsto.cauchy_map {l : Filter Î²} [NeBot l] {f : Î² â†’ Î±} {a : Î±}
    (h : Tendsto f l (ğ“ a)) : Cauchy (map f l) :=
  cauchy_nhds.mono h
#align filter.tendsto.cauchy_map Filter.Tendsto.cauchy_map

lemma Cauchy.mono_uniformSpace {u v : UniformSpace Î²} {F : Filter Î²} (huv : u â‰¤ v)
    (hF : Cauchy (uniformSpace := u) F) : Cauchy (uniformSpace := v) F :=
  âŸ¨hF.1, hF.2.trans huvâŸ©

lemma cauchy_inf_uniformSpace {u v : UniformSpace Î²} {F : Filter Î²} :
    Cauchy (uniformSpace := u âŠ“ v) F â†”
    Cauchy (uniformSpace := u) F âˆ§ Cauchy (uniformSpace := v) F := by
  unfold Cauchy
  rw [inf_uniformity (u := u), le_inf_iff, and_and_left]

lemma cauchy_iInf_uniformSpace {Î¹ : Sort*} [Nonempty Î¹] {u : Î¹ â†’ UniformSpace Î²}
    {l : Filter Î²} :
    Cauchy (uniformSpace := â¨… i, u i) l â†” âˆ€ i, Cauchy (uniformSpace := u i) l := by
  unfold Cauchy
  rw [iInf_uniformity, le_iInf_iff, forall_and, forall_const]

lemma cauchy_iInf_uniformSpace' {Î¹ : Sort*} {u : Î¹ â†’ UniformSpace Î²}
    {l : Filter Î²} [l.NeBot] :
    Cauchy (uniformSpace := â¨… i, u i) l â†” âˆ€ i, Cauchy (uniformSpace := u i) l := by
  simp_rw [cauchy_iff_le (uniformSpace := _), iInf_uniformity, le_iInf_iff]

lemma cauchy_comap_uniformSpace {u : UniformSpace Î²} {f : Î± â†’ Î²} {l : Filter Î±} :
    Cauchy (uniformSpace := comap f u) l â†” Cauchy (map f l) := by
  simp only [Cauchy, map_neBot_iff, prod_map_map_eq, map_le_iff_le_comap]
  rfl

lemma cauchy_prod_iff [UniformSpace Î²] {F : Filter (Î± Ã— Î²)} :
    Cauchy F â†” Cauchy (map Prod.fst F) âˆ§ Cauchy (map Prod.snd F) := by
  simp_rw [instUniformSpaceProd, â† cauchy_comap_uniformSpace, â† cauchy_inf_uniformSpace]

theorem Cauchy.prod [UniformSpace Î²] {f : Filter Î±} {g : Filter Î²} (hf : Cauchy f) (hg : Cauchy g) :
    Cauchy (f Ã—Ë¢ g) := by
  have := hf.1; have := hg.1
  simpa [cauchy_prod_iff, hf.1] using âŸ¨hf, hgâŸ©
#align cauchy.prod Cauchy.prod

/-- The common part of the proofs of `le_nhds_of_cauchy_adhp` and
`SequentiallyComplete.le_nhds_of_seq_tendsto_nhds`: if for any entourage `s`
one can choose a set `t âˆˆ f` of diameter `s` such that it contains a point `y`
with `(x, y) âˆˆ s`, then `f` converges to `x`. -/
theorem le_nhds_of_cauchy_adhp_aux {f : Filter Î±} {x : Î±}
    (adhs : âˆ€ s âˆˆ ğ“¤ Î±, âˆƒ t âˆˆ f, t Ã—Ë¢ t âŠ† s âˆ§ âˆƒ y, (x, y) âˆˆ s âˆ§ y âˆˆ t) : f â‰¤ ğ“ x := by
  -- Consider a neighborhood `s` of `x`
  intro s hs
  -- Take an entourage twice smaller than `s`
  rcases comp_mem_uniformity_sets (mem_nhds_uniformity_iff_right.1 hs) with âŸ¨U, U_mem, hUâŸ©
  -- Take a set `t âˆˆ f`, `t Ã— t âŠ† U`, and a point `y âˆˆ t` such that `(x, y) âˆˆ U`
  rcases adhs U U_mem with âŸ¨t, t_mem, ht, y, hxy, hyâŸ©
  apply mem_of_superset t_mem
  -- Given a point `z âˆˆ t`, we have `(x, y) âˆˆ U` and `(y, z) âˆˆ t Ã— t âŠ† U`, hence `z âˆˆ s`
  exact fun z hz => hU (prod_mk_mem_compRel hxy (ht <| mk_mem_prod hy hz)) rfl
#align le_nhds_of_cauchy_adhp_aux le_nhds_of_cauchy_adhp_aux

/-- If `x` is an adherent (cluster) point for a Cauchy filter `f`, then it is a limit point
for `f`. -/
theorem le_nhds_of_cauchy_adhp {f : Filter Î±} {x : Î±} (hf : Cauchy f) (adhs : ClusterPt x f) :
    f â‰¤ ğ“ x :=
  le_nhds_of_cauchy_adhp_aux
    (fun s hs => by
      obtain âŸ¨t, t_mem, htâŸ© : âˆƒ t âˆˆ f, t Ã—Ë¢ t âŠ† s := (cauchy_iff.1 hf).2 s hs
      use t, t_mem, ht
      exact forall_mem_nonempty_iff_neBot.2 adhs _ (inter_mem_inf (mem_nhds_left x hs) t_mem))
#align le_nhds_of_cauchy_adhp le_nhds_of_cauchy_adhp

theorem le_nhds_iff_adhp_of_cauchy {f : Filter Î±} {x : Î±} (hf : Cauchy f) :
    f â‰¤ ğ“ x â†” ClusterPt x f :=
  âŸ¨fun h => ClusterPt.of_le_nhds' h hf.1, le_nhds_of_cauchy_adhp hfâŸ©
#align le_nhds_iff_adhp_of_cauchy le_nhds_iff_adhp_of_cauchy

nonrec theorem Cauchy.map [UniformSpace Î²] {f : Filter Î±} {m : Î± â†’ Î²} (hf : Cauchy f)
    (hm : UniformContinuous m) : Cauchy (map m f) :=
  âŸ¨hf.1.map _,
    calc
      map m f Ã—Ë¢ map m f = map (Prod.map m m) (f Ã—Ë¢ f) := Filter.prod_map_map_eq
      _ â‰¤ Filter.map (Prod.map m m) (ğ“¤ Î±) := map_mono hf.right
      _ â‰¤ ğ“¤ Î² := hmâŸ©
#align cauchy.map Cauchy.map

nonrec theorem Cauchy.comap [UniformSpace Î²] {f : Filter Î²} {m : Î± â†’ Î²} (hf : Cauchy f)
    (hm : comap (fun p : Î± Ã— Î± => (m p.1, m p.2)) (ğ“¤ Î²) â‰¤ ğ“¤ Î±) [NeBot (comap m f)] :
    Cauchy (comap m f) :=
  âŸ¨â€¹_â€º,
    calc
      comap m f Ã—Ë¢ comap m f = comap (Prod.map m m) (f Ã—Ë¢ f) := prod_comap_comap_eq
      _ â‰¤ comap (Prod.map m m) (ğ“¤ Î²) := comap_mono hf.right
      _ â‰¤ ğ“¤ Î± := hmâŸ©
#align cauchy.comap Cauchy.comap

theorem Cauchy.comap' [UniformSpace Î²] {f : Filter Î²} {m : Î± â†’ Î²} (hf : Cauchy f)
    (hm : Filter.comap (fun p : Î± Ã— Î± => (m p.1, m p.2)) (ğ“¤ Î²) â‰¤ ğ“¤ Î±)
    (_ : NeBot (Filter.comap m f)) : Cauchy (Filter.comap m f) :=
  hf.comap hm
#align cauchy.comap' Cauchy.comap'

/-- Cauchy sequences. Usually defined on â„•, but often it is also useful to say that a function
defined on â„ is Cauchy at +âˆ to deduce convergence. Therefore, we define it in a type class that
is general enough to cover both â„• and â„, which are the main motivating examples. -/
def CauchySeq [Preorder Î²] (u : Î² â†’ Î±) :=
  Cauchy (atTop.map u)
#align cauchy_seq CauchySeq

theorem CauchySeq.tendsto_uniformity [Preorder Î²] {u : Î² â†’ Î±} (h : CauchySeq u) :
    Tendsto (Prod.map u u) atTop (ğ“¤ Î±) := by
  simpa only [Tendsto, prod_map_map_eq', prod_atTop_atTop_eq] using h.right
#align cauchy_seq.tendsto_uniformity CauchySeq.tendsto_uniformity

theorem CauchySeq.nonempty [Preorder Î²] {u : Î² â†’ Î±} (hu : CauchySeq u) : Nonempty Î² :=
  @nonempty_of_neBot _ _ <| (map_neBot_iff _).1 hu.1
#align cauchy_seq.nonempty CauchySeq.nonempty

theorem CauchySeq.mem_entourage {Î² : Type*} [SemilatticeSup Î²] {u : Î² â†’ Î±} (h : CauchySeq u)
    {V : Set (Î± Ã— Î±)} (hV : V âˆˆ ğ“¤ Î±) : âˆƒ kâ‚€, âˆ€ i j, kâ‚€ â‰¤ i â†’ kâ‚€ â‰¤ j â†’ (u i, u j) âˆˆ V := by
  haveI := h.nonempty
  have := h.tendsto_uniformity; rw [â† prod_atTop_atTop_eq] at this
  simpa [MapsTo] using atTop_basis.prod_self.tendsto_left_iff.1 this V hV
#align cauchy_seq.mem_entourage CauchySeq.mem_entourage

theorem Filter.Tendsto.cauchySeq [SemilatticeSup Î²] [Nonempty Î²] {f : Î² â†’ Î±} {x}
    (hx : Tendsto f atTop (ğ“ x)) : CauchySeq f :=
  hx.cauchy_map
#align filter.tendsto.cauchy_seq Filter.Tendsto.cauchySeq

theorem cauchySeq_const [SemilatticeSup Î²] [Nonempty Î²] (x : Î±) : CauchySeq fun _ : Î² => x :=
  tendsto_const_nhds.cauchySeq
#align cauchy_seq_const cauchySeq_const

theorem cauchySeq_iff_tendsto [Nonempty Î²] [SemilatticeSup Î²] {u : Î² â†’ Î±} :
    CauchySeq u â†” Tendsto (Prod.map u u) atTop (ğ“¤ Î±) :=
  cauchy_map_iff'.trans <| by simp only [prod_atTop_atTop_eq, Prod.map_def]
#align cauchy_seq_iff_tendsto cauchySeq_iff_tendsto

theorem CauchySeq.comp_tendsto {Î³} [Preorder Î²] [SemilatticeSup Î³] [Nonempty Î³] {f : Î² â†’ Î±}
    (hf : CauchySeq f) {g : Î³ â†’ Î²} (hg : Tendsto g atTop atTop) : CauchySeq (f âˆ˜ g) :=
  âŸ¨inferInstance, le_trans (prod_le_prod.mpr âŸ¨Tendsto.comp le_rfl hg, Tendsto.comp le_rfl hgâŸ©) hf.2âŸ©
#align cauchy_seq.comp_tendsto CauchySeq.comp_tendsto

theorem CauchySeq.comp_injective [SemilatticeSup Î²] [NoMaxOrder Î²] [Nonempty Î²] {u : â„• â†’ Î±}
    (hu : CauchySeq u) {f : Î² â†’ â„•} (hf : Injective f) : CauchySeq (u âˆ˜ f) :=
  hu.comp_tendsto <| Nat.cofinite_eq_atTop â–¸ hf.tendsto_cofinite.mono_left atTop_le_cofinite
#align cauchy_seq.comp_injective CauchySeq.comp_injective

theorem Function.Bijective.cauchySeq_comp_iff {f : â„• â†’ â„•} (hf : Bijective f) (u : â„• â†’ Î±) :
    CauchySeq (u âˆ˜ f) â†” CauchySeq u := by
  refine âŸ¨fun H => ?_, fun H => H.comp_injective hf.injectiveâŸ©
  lift f to â„• â‰ƒ â„• using hf
  simpa only [(Â· âˆ˜ Â·), f.apply_symm_apply] using H.comp_injective f.symm.injective
#align function.bijective.cauchy_seq_comp_iff Function.Bijective.cauchySeq_comp_iff

theorem CauchySeq.subseq_subseq_mem {V : â„• â†’ Set (Î± Ã— Î±)} (hV : âˆ€ n, V n âˆˆ ğ“¤ Î±) {u : â„• â†’ Î±}
    (hu : CauchySeq u) {f g : â„• â†’ â„•} (hf : Tendsto f atTop atTop) (hg : Tendsto g atTop atTop) :
    âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ âˆ€ n, ((u âˆ˜ f âˆ˜ Ï†) n, (u âˆ˜ g âˆ˜ Ï†) n) âˆˆ V n := by
  rw [cauchySeq_iff_tendsto] at hu
  exact ((hu.comp <| hf.prod_atTop hg).comp tendsto_atTop_diagonal).subseq_mem hV
#align cauchy_seq.subseq_subseq_mem CauchySeq.subseq_subseq_mem

-- todo: generalize this and other lemmas to a nonempty semilattice
theorem cauchySeq_iff' {u : â„• â†’ Î±} :
    CauchySeq u â†” âˆ€ V âˆˆ ğ“¤ Î±, âˆ€á¶  k in atTop, k âˆˆ Prod.map u u â»Â¹' V :=
  cauchySeq_iff_tendsto
#align cauchy_seq_iff' cauchySeq_iff'

theorem cauchySeq_iff {u : â„• â†’ Î±} :
    CauchySeq u â†” âˆ€ V âˆˆ ğ“¤ Î±, âˆƒ N, âˆ€ k â‰¥ N, âˆ€ l â‰¥ N, (u k, u l) âˆˆ V := by
  simp only [cauchySeq_iff', Filter.eventually_atTop_prod_self', mem_preimage, Prod_map]
#align cauchy_seq_iff cauchySeq_iff

theorem CauchySeq.prod_map {Î³ Î´} [UniformSpace Î²] [Preorder Î³] [Preorder Î´] {u : Î³ â†’ Î±}
    {v : Î´ â†’ Î²} (hu : CauchySeq u) (hv : CauchySeq v) : CauchySeq (Prod.map u v) := by
  simpa only [CauchySeq, prod_map_map_eq', prod_atTop_atTop_eq] using hu.prod hv
#align cauchy_seq.prod_map CauchySeq.prod_map

theorem CauchySeq.prod {Î³} [UniformSpace Î²] [Preorder Î³] {u : Î³ â†’ Î±} {v : Î³ â†’ Î²}
    (hu : CauchySeq u) (hv : CauchySeq v) : CauchySeq fun x => (u x, v x) :=
  haveI := hu.1.of_map
  (Cauchy.prod hu hv).mono (Tendsto.prod_mk le_rfl le_rfl)
#align cauchy_seq.prod CauchySeq.prod

theorem CauchySeq.eventually_eventually [SemilatticeSup Î²] {u : Î² â†’ Î±} (hu : CauchySeq u)
    {V : Set (Î± Ã— Î±)} (hV : V âˆˆ ğ“¤ Î±) : âˆ€á¶  k in atTop, âˆ€á¶  l in atTop, (u k, u l) âˆˆ V :=
  eventually_atTop_curry <| hu.tendsto_uniformity hV
#align cauchy_seq.eventually_eventually CauchySeq.eventually_eventually

theorem UniformContinuous.comp_cauchySeq {Î³} [UniformSpace Î²] [Preorder Î³] {f : Î± â†’ Î²}
    (hf : UniformContinuous f) {u : Î³ â†’ Î±} (hu : CauchySeq u) : CauchySeq (f âˆ˜ u) :=
  hu.map hf
#align uniform_continuous.comp_cauchy_seq UniformContinuous.comp_cauchySeq

theorem CauchySeq.subseq_mem {V : â„• â†’ Set (Î± Ã— Î±)} (hV : âˆ€ n, V n âˆˆ ğ“¤ Î±) {u : â„• â†’ Î±}
    (hu : CauchySeq u) : âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ âˆ€ n, (u <| Ï† (n + 1), u <| Ï† n) âˆˆ V n := by
  have : âˆ€ n, âˆƒ N, âˆ€ k â‰¥ N, âˆ€ l â‰¥ k, (u l, u k) âˆˆ V n := fun n => by
    rw [cauchySeq_iff] at hu
    rcases hu _ (hV n) with âŸ¨N, HâŸ©
    exact âŸ¨N, fun k hk l hl => H _ (le_trans hk hl) _ hkâŸ©
  obtain âŸ¨Ï† : â„• â†’ â„•, Ï†_extr : StrictMono Ï†, hÏ† : âˆ€ n, âˆ€ l â‰¥ Ï† n, (u l, u <| Ï† n) âˆˆ V nâŸ© :=
    extraction_forall_of_eventually' this
  exact âŸ¨Ï†, Ï†_extr, fun n => hÏ† _ _ (Ï†_extr <| lt_add_one n).leâŸ©
#align cauchy_seq.subseq_mem CauchySeq.subseq_mem

theorem Filter.Tendsto.subseq_mem_entourage {V : â„• â†’ Set (Î± Ã— Î±)} (hV : âˆ€ n, V n âˆˆ ğ“¤ Î±) {u : â„• â†’ Î±}
    {a : Î±} (hu : Tendsto u atTop (ğ“ a)) : âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ (u (Ï† 0), a) âˆˆ V 0 âˆ§
      âˆ€ n, (u <| Ï† (n + 1), u <| Ï† n) âˆˆ V (n + 1) := by
  rcases mem_atTop_sets.1 (hu (ball_mem_nhds a (symm_le_uniformity <| hV 0))) with âŸ¨n, hnâŸ©
  rcases (hu.comp (tendsto_add_atTop_nat n)).cauchySeq.subseq_mem fun n => hV (n + 1) with
    âŸ¨Ï†, Ï†_mono, hÏ†VâŸ©
  exact âŸ¨fun k => Ï† k + n, Ï†_mono.add_const _, hn _ le_add_self, hÏ†VâŸ©
#align filter.tendsto.subseq_mem_entourage Filter.Tendsto.subseq_mem_entourage

/-- If a Cauchy sequence has a convergent subsequence, then it converges. -/
theorem tendsto_nhds_of_cauchySeq_of_subseq [Preorder Î²] {u : Î² â†’ Î±} (hu : CauchySeq u)
    {Î¹ : Type*} {f : Î¹ â†’ Î²} {p : Filter Î¹} [NeBot p] (hf : Tendsto f p atTop) {a : Î±}
    (ha : Tendsto (u âˆ˜ f) p (ğ“ a)) : Tendsto u atTop (ğ“ a) :=
  le_nhds_of_cauchy_adhp hu (mapClusterPt_of_comp hf ha)
#align tendsto_nhds_of_cauchy_seq_of_subseq tendsto_nhds_of_cauchySeq_of_subseq

/-- Any shift of a Cauchy sequence is also a Cauchy sequence. -/
theorem cauchySeq_shift {u : â„• â†’ Î±} (k : â„•) : CauchySeq (fun n â†¦ u (n + k)) â†” CauchySeq u := by
  constructor <;> intro h
  Â· rw [cauchySeq_iff] at h âŠ¢
    intro V mV
    obtain âŸ¨N, hâŸ© := h V mV
    use N + k
    intro a ha b hb
    convert h (a - k) (Nat.le_sub_of_add_le ha) (b - k) (Nat.le_sub_of_add_le hb) <;> omega
  Â· exact h.comp_tendsto (tendsto_add_atTop_nat k)

theorem Filter.HasBasis.cauchySeq_iff {Î³} [Nonempty Î²] [SemilatticeSup Î²] {u : Î² â†’ Î±} {p : Î³ â†’ Prop}
    {s : Î³ â†’ Set (Î± Ã— Î±)} (h : (ğ“¤ Î±).HasBasis p s) :
    CauchySeq u â†” âˆ€ i, p i â†’ âˆƒ N, âˆ€ m, N â‰¤ m â†’ âˆ€ n, N â‰¤ n â†’ (u m, u n) âˆˆ s i := by
  rw [cauchySeq_iff_tendsto, â† prod_atTop_atTop_eq]
  refine (atTop_basis.prod_self.tendsto_iff h).trans ?_
  simp only [exists_prop, true_and_iff, MapsTo, preimage, subset_def, Prod.forall, mem_prod_eq,
    mem_setOf_eq, mem_Ici, and_imp, Prod.map, ge_iff_le, @forall_swap (_ â‰¤ _) Î²]
#align filter.has_basis.cauchy_seq_iff Filter.HasBasis.cauchySeq_iff

theorem Filter.HasBasis.cauchySeq_iff' {Î³} [Nonempty Î²] [SemilatticeSup Î²] {u : Î² â†’ Î±}
    {p : Î³ â†’ Prop} {s : Î³ â†’ Set (Î± Ã— Î±)} (H : (ğ“¤ Î±).HasBasis p s) :
    CauchySeq u â†” âˆ€ i, p i â†’ âˆƒ N, âˆ€ n â‰¥ N, (u n, u N) âˆˆ s i := by
  refine H.cauchySeq_iff.trans âŸ¨fun h i hi => ?_, fun h i hi => ?_âŸ©
  Â· exact (h i hi).imp fun N hN n hn => hN n hn N le_rfl
  Â· rcases comp_symm_of_uniformity (H.mem_of_mem hi) with âŸ¨t, ht, ht', htsâŸ©
    rcases H.mem_iff.1 ht with âŸ¨j, hj, hjtâŸ©
    refine (h j hj).imp fun N hN m hm n hn => hts âŸ¨u N, hjt ?_, ht' <| hjt ?_âŸ©
    exacts [hN m hm, hN n hn]
#align filter.has_basis.cauchy_seq_iff' Filter.HasBasis.cauchySeq_iff'

theorem cauchySeq_of_controlled [SemilatticeSup Î²] [Nonempty Î²] (U : Î² â†’ Set (Î± Ã— Î±))
    (hU : âˆ€ s âˆˆ ğ“¤ Î±, âˆƒ n, U n âŠ† s) {f : Î² â†’ Î±}
    (hf : âˆ€ â¦ƒN m n : Î²â¦„, N â‰¤ m â†’ N â‰¤ n â†’ (f m, f n) âˆˆ U N) : CauchySeq f :=
    -- Porting note: changed to semi-implicit arguments
  cauchySeq_iff_tendsto.2
    (by
      intro s hs
      rw [mem_map, mem_atTop_sets]
      cases' hU s hs with N hN
      refine âŸ¨(N, N), fun mn hmn => ?_âŸ©
      cases' mn with m n
      exact hN (hf hmn.1 hmn.2))
#align cauchy_seq_of_controlled cauchySeq_of_controlled

theorem isComplete_iff_clusterPt {s : Set Î±} :
    IsComplete s â†” âˆ€ l, Cauchy l â†’ l â‰¤ ğ“Ÿ s â†’ âˆƒ x âˆˆ s, ClusterPt x l :=
  forallâ‚ƒ_congr fun _ hl _ => exists_congr fun _ => and_congr_right fun _ =>
    le_nhds_iff_adhp_of_cauchy hl
#align is_complete_iff_cluster_pt isComplete_iff_clusterPt

theorem isComplete_iff_ultrafilter {s : Set Î±} :
    IsComplete s â†” âˆ€ l : Ultrafilter Î±, Cauchy (l : Filter Î±) â†’ â†‘l â‰¤ ğ“Ÿ s â†’ âˆƒ x âˆˆ s, â†‘l â‰¤ ğ“ x := by
  refine âŸ¨fun h l => h l, fun H => isComplete_iff_clusterPt.2 fun l hl hls => ?_âŸ©
  haveI := hl.1
  rcases H (Ultrafilter.of l) hl.ultrafilter_of ((Ultrafilter.of_le l).trans hls) with âŸ¨x, hxs, hxlâŸ©
  exact âŸ¨x, hxs, (ClusterPt.of_le_nhds hxl).mono (Ultrafilter.of_le l)âŸ©
#align is_complete_iff_ultrafilter isComplete_iff_ultrafilter

theorem isComplete_iff_ultrafilter' {s : Set Î±} :
    IsComplete s â†” âˆ€ l : Ultrafilter Î±, Cauchy (l : Filter Î±) â†’ s âˆˆ l â†’ âˆƒ x âˆˆ s, â†‘l â‰¤ ğ“ x :=
  isComplete_iff_ultrafilter.trans <| by simp only [le_principal_iff, Ultrafilter.mem_coe]
#align is_complete_iff_ultrafilter' isComplete_iff_ultrafilter'

protected theorem IsComplete.union {s t : Set Î±} (hs : IsComplete s) (ht : IsComplete t) :
    IsComplete (s âˆª t) := by
  simp only [isComplete_iff_ultrafilter', Ultrafilter.union_mem_iff, or_imp] at *
  exact fun l hl =>
    âŸ¨fun hsl => (hs l hl hsl).imp fun x hx => âŸ¨Or.inl hx.1, hx.2âŸ©, fun htl =>
      (ht l hl htl).imp fun x hx => âŸ¨Or.inr hx.1, hx.2âŸ©âŸ©
#align is_complete.union IsComplete.union

theorem isComplete_iUnion_separated {Î¹ : Sort*} {s : Î¹ â†’ Set Î±} (hs : âˆ€ i, IsComplete (s i))
    {U : Set (Î± Ã— Î±)} (hU : U âˆˆ ğ“¤ Î±) (hd : âˆ€ (i j : Î¹), âˆ€ x âˆˆ s i, âˆ€ y âˆˆ s j, (x, y) âˆˆ U â†’ i = j) :
    IsComplete (â‹ƒ i, s i) := by
  set S := â‹ƒ i, s i
  intro l hl hls
  rw [le_principal_iff] at hls
  cases' cauchy_iff.1 hl with hl_ne hl'
  obtain âŸ¨t, htS, htl, htUâŸ© : âˆƒ t, t âŠ† S âˆ§ t âˆˆ l âˆ§ t Ã—Ë¢ t âŠ† U := by
    rcases hl' U hU with âŸ¨t, htl, htUâŸ©
    refine âŸ¨t âˆ© S, inter_subset_right _ _, inter_mem htl hls, Subset.trans ?_ htUâŸ©
    gcongr <;> apply inter_subset_left
  obtain âŸ¨i, hiâŸ© : âˆƒ i, t âŠ† s i := by
    rcases Filter.nonempty_of_mem htl with âŸ¨x, hxâŸ©
    rcases mem_iUnion.1 (htS hx) with âŸ¨i, hiâŸ©
    refine âŸ¨i, fun y hy => ?_âŸ©
    rcases mem_iUnion.1 (htS hy) with âŸ¨j, hjâŸ©
    rwa [hd i j x hi y hj (htU <| mk_mem_prod hx hy)]
  rcases hs i l hl (le_principal_iff.2 <| mem_of_superset htl hi) with âŸ¨x, hxs, hlxâŸ©
  exact âŸ¨x, mem_iUnion.2 âŸ¨i, hxsâŸ©, hlxâŸ©
#align is_complete_Union_separated isComplete_iUnion_separated

/-- A complete space is defined here using uniformities. A uniform space
  is complete if every Cauchy filter converges. -/
class CompleteSpace (Î± : Type u) [UniformSpace Î±] : Prop where
  /-- In a complete uniform space, every Cauchy filter converges. -/
  complete : âˆ€ {f : Filter Î±}, Cauchy f â†’ âˆƒ x, f â‰¤ ğ“ x
#align complete_space CompleteSpace

theorem complete_univ {Î± : Type u} [UniformSpace Î±] [CompleteSpace Î±] :
    IsComplete (univ : Set Î±) := fun f hf _ => by
  rcases CompleteSpace.complete hf with âŸ¨x, hxâŸ©
  exact âŸ¨x, mem_univ x, hxâŸ©
#align complete_univ complete_univ

instance CompleteSpace.prod [UniformSpace Î²] [CompleteSpace Î±] [CompleteSpace Î²] :
    CompleteSpace (Î± Ã— Î²) where
  complete hf :=
    let âŸ¨x1, hx1âŸ© := CompleteSpace.complete <| hf.map uniformContinuous_fst
    let âŸ¨x2, hx2âŸ© := CompleteSpace.complete <| hf.map uniformContinuous_snd
    âŸ¨(x1, x2), by rw [nhds_prod_eq, le_prod]; constructor <;> assumptionâŸ©
#align complete_space.prod CompleteSpace.prod

lemma CompleteSpace.fst_of_prod [UniformSpace Î²] [CompleteSpace (Î± Ã— Î²)] [h : Nonempty Î²] :
    CompleteSpace Î± where
  complete hf :=
    let âŸ¨yâŸ© := h
    let âŸ¨(a, b), habâŸ© := CompleteSpace.complete <| hf.prod <| cauchy_pure (a := y)
    âŸ¨a, by simpa only [map_fst_prod, nhds_prod_eq] using map_mono (m := Prod.fst) habâŸ©

lemma CompleteSpace.snd_of_prod [UniformSpace Î²] [CompleteSpace (Î± Ã— Î²)] [h : Nonempty Î±] :
    CompleteSpace Î² where
  complete hf :=
    let âŸ¨xâŸ© := h
    let âŸ¨(a, b), habâŸ© := CompleteSpace.complete <| (cauchy_pure (a := x)).prod hf
    âŸ¨b, by simpa only [map_snd_prod, nhds_prod_eq] using map_mono (m := Prod.snd) habâŸ©

lemma completeSpace_prod_of_nonempty [UniformSpace Î²] [Nonempty Î±] [Nonempty Î²] :
    CompleteSpace (Î± Ã— Î²) â†” CompleteSpace Î± âˆ§ CompleteSpace Î² :=
  âŸ¨fun _ â†¦ âŸ¨.fst_of_prod (Î² := Î²), .snd_of_prod (Î± := Î±)âŸ©, fun âŸ¨_, _âŸ© â†¦ .prodâŸ©

@[to_additive]
instance CompleteSpace.mulOpposite [CompleteSpace Î±] : CompleteSpace Î±áµáµ’áµ– where
  complete hf :=
    MulOpposite.op_surjective.exists.mpr <|
      let âŸ¨x, hxâŸ© := CompleteSpace.complete (hf.map MulOpposite.uniformContinuous_unop)
      âŸ¨x, (map_le_iff_le_comap.mp hx).trans_eq <| MulOpposite.comap_unop_nhds _âŸ©
#align complete_space.mul_opposite CompleteSpace.mulOpposite
#align complete_space.add_opposite CompleteSpace.addOpposite

/-- If `univ` is complete, the space is a complete space -/
theorem completeSpace_of_isComplete_univ (h : IsComplete (univ : Set Î±)) : CompleteSpace Î± :=
  âŸ¨fun hf => let âŸ¨x, _, hxâŸ© := h _ hf ((@principal_univ Î±).symm â–¸ le_top); âŸ¨x, hxâŸ©âŸ©
#align complete_space_of_is_complete_univ completeSpace_of_isComplete_univ

theorem completeSpace_iff_isComplete_univ : CompleteSpace Î± â†” IsComplete (univ : Set Î±) :=
  âŸ¨@complete_univ Î± _, completeSpace_of_isComplete_univâŸ©
#align complete_space_iff_is_complete_univ completeSpace_iff_isComplete_univ

theorem completeSpace_iff_ultrafilter :
    CompleteSpace Î± â†” âˆ€ l : Ultrafilter Î±, Cauchy (l : Filter Î±) â†’ âˆƒ x : Î±, â†‘l â‰¤ ğ“ x := by
  simp [completeSpace_iff_isComplete_univ, isComplete_iff_ultrafilter]
#align complete_space_iff_ultrafilter completeSpace_iff_ultrafilter

theorem cauchy_iff_exists_le_nhds [CompleteSpace Î±] {l : Filter Î±} [NeBot l] :
    Cauchy l â†” âˆƒ x, l â‰¤ ğ“ x :=
  âŸ¨CompleteSpace.complete, fun âŸ¨_, hxâŸ© => cauchy_nhds.mono hxâŸ©
#align cauchy_iff_exists_le_nhds cauchy_iff_exists_le_nhds

theorem cauchy_map_iff_exists_tendsto [CompleteSpace Î±] {l : Filter Î²} {f : Î² â†’ Î±} [NeBot l] :
    Cauchy (l.map f) â†” âˆƒ x, Tendsto f l (ğ“ x) :=
  cauchy_iff_exists_le_nhds
#align cauchy_map_iff_exists_tendsto cauchy_map_iff_exists_tendsto

/-- A Cauchy sequence in a complete space converges -/
theorem cauchySeq_tendsto_of_complete [Preorder Î²] [CompleteSpace Î±] {u : Î² â†’ Î±}
    (H : CauchySeq u) : âˆƒ x, Tendsto u atTop (ğ“ x) :=
  CompleteSpace.complete H
#align cauchy_seq_tendsto_of_complete cauchySeq_tendsto_of_complete

/-- If `K` is a complete subset, then any cauchy sequence in `K` converges to a point in `K` -/
theorem cauchySeq_tendsto_of_isComplete [Preorder Î²] {K : Set Î±} (hâ‚ : IsComplete K)
    {u : Î² â†’ Î±} (hâ‚‚ : âˆ€ n, u n âˆˆ K) (hâ‚ƒ : CauchySeq u) : âˆƒ v âˆˆ K, Tendsto u atTop (ğ“ v) :=
  hâ‚ _ hâ‚ƒ <| le_principal_iff.2 <| mem_map_iff_exists_image.2
    âŸ¨univ, univ_mem, by rwa [image_univ, range_subset_iff]âŸ©
#align cauchy_seq_tendsto_of_is_complete cauchySeq_tendsto_of_isComplete

theorem Cauchy.le_nhds_lim [CompleteSpace Î±] {f : Filter Î±} (hf : Cauchy f) :
    haveI := hf.1.nonempty; f â‰¤ ğ“ (lim f) :=
  _root_.le_nhds_lim (CompleteSpace.complete hf)
set_option linter.uppercaseLean3 false in
#align cauchy.le_nhds_Lim Cauchy.le_nhds_lim

theorem CauchySeq.tendsto_limUnder [Preorder Î²] [CompleteSpace Î±] {u : Î² â†’ Î±} (h : CauchySeq u) :
    haveI := h.1.nonempty; Tendsto u atTop (ğ“ <| limUnder atTop u) :=
  h.le_nhds_lim
#align cauchy_seq.tendsto_lim CauchySeq.tendsto_limUnder

theorem IsClosed.isComplete [CompleteSpace Î±] {s : Set Î±} (h : IsClosed s) : IsComplete s :=
  fun _ cf fs =>
  let âŸ¨x, hxâŸ© := CompleteSpace.complete cf
  âŸ¨x, isClosed_iff_clusterPt.mp h x (cf.left.mono (le_inf hx fs)), hxâŸ©
#align is_closed.is_complete IsClosed.isComplete

/-- A set `s` is totally bounded if for every entourage `d` there is a finite
  set of points `t` such that every element of `s` is `d`-near to some element of `t`. -/
def TotallyBounded (s : Set Î±) : Prop :=
  âˆ€ d âˆˆ ğ“¤ Î±, âˆƒ t : Set Î±, t.Finite âˆ§ s âŠ† â‹ƒ y âˆˆ t, { x | (x, y) âˆˆ d }
#align totally_bounded TotallyBounded

theorem TotallyBounded.exists_subset {s : Set Î±} (hs : TotallyBounded s) {U : Set (Î± Ã— Î±)}
    (hU : U âˆˆ ğ“¤ Î±) : âˆƒ t, t âŠ† s âˆ§ Set.Finite t âˆ§ s âŠ† â‹ƒ y âˆˆ t, { x | (x, y) âˆˆ U } := by
  rcases comp_symm_of_uniformity hU with âŸ¨r, hr, rs, rUâŸ©
  rcases hs r hr with âŸ¨k, fk, ksâŸ©
  let u := k âˆ© { y | âˆƒ x âˆˆ s, (x, y) âˆˆ r }
  choose f hfs hfr using fun x : u => x.coe_prop.2
  refine âŸ¨range f, ?_, ?_, ?_âŸ©
  Â· exact range_subset_iff.2 hfs
  Â· haveI : Fintype u := (fk.inter_of_left _).fintype
    exact finite_range f
  Â· intro x xs
    obtain âŸ¨y, hy, xyâŸ© := mem_iUnionâ‚‚.1 (ks xs)
    rw [biUnion_range, mem_iUnion]
    set z : â†¥u := âŸ¨y, hy, âŸ¨x, xs, xyâŸ©âŸ©
    exact âŸ¨z, rU <| mem_compRel.2 âŸ¨y, xy, rs (hfr z)âŸ©âŸ©
#align totally_bounded.exists_subset TotallyBounded.exists_subset

theorem totallyBounded_iff_subset {s : Set Î±} :
    TotallyBounded s â†”
      âˆ€ d âˆˆ ğ“¤ Î±, âˆƒ t, t âŠ† s âˆ§ Set.Finite t âˆ§ s âŠ† â‹ƒ y âˆˆ t, { x | (x, y) âˆˆ d } :=
  âŸ¨fun H _ hd => H.exists_subset hd, fun H d hd =>
    let âŸ¨t, _, htâŸ© := H d hd
    âŸ¨t, htâŸ©âŸ©
#align totally_bounded_iff_subset totallyBounded_iff_subset

theorem Filter.HasBasis.totallyBounded_iff {Î¹} {p : Î¹ â†’ Prop} {U : Î¹ â†’ Set (Î± Ã— Î±)}
    (H : (ğ“¤ Î±).HasBasis p U) {s : Set Î±} :
    TotallyBounded s â†” âˆ€ i, p i â†’ âˆƒ t : Set Î±, Set.Finite t âˆ§ s âŠ† â‹ƒ y âˆˆ t, { x | (x, y) âˆˆ U i } :=
  H.forall_iff fun _ _ hUV h =>
    h.imp fun _ ht => âŸ¨ht.1, ht.2.trans <| iUnionâ‚‚_mono fun _ _ _ hy => hUV hyâŸ©
#align filter.has_basis.totally_bounded_iff Filter.HasBasis.totallyBounded_iff

theorem totallyBounded_of_forall_symm {s : Set Î±}
    (h : âˆ€ V âˆˆ ğ“¤ Î±, SymmetricRel V â†’ âˆƒ t : Set Î±, Set.Finite t âˆ§ s âŠ† â‹ƒ y âˆˆ t, ball y V) :
    TotallyBounded s :=
  UniformSpace.hasBasis_symmetric.totallyBounded_iff.2 fun V hV => by
    simpa only [ball_eq_of_symmetry hV.2] using h V hV.1 hV.2
#align totally_bounded_of_forall_symm totallyBounded_of_forall_symm

theorem totallyBounded_subset {sâ‚ sâ‚‚ : Set Î±} (hs : sâ‚ âŠ† sâ‚‚) (h : TotallyBounded sâ‚‚) :
    TotallyBounded sâ‚ := fun d hd =>
  let âŸ¨t, htâ‚, htâ‚‚âŸ© := h d hd
  âŸ¨t, htâ‚, Subset.trans hs htâ‚‚âŸ©
#align totally_bounded_subset totallyBounded_subset

theorem totallyBounded_empty : TotallyBounded (âˆ… : Set Î±) := fun _ _ =>
  âŸ¨âˆ…, finite_empty, empty_subset _âŸ©
#align totally_bounded_empty totallyBounded_empty

/-- The closure of a totally bounded set is totally bounded. -/
theorem TotallyBounded.closure {s : Set Î±} (h : TotallyBounded s) : TotallyBounded (closure s) :=
  uniformity_hasBasis_closed.totallyBounded_iff.2 fun V hV =>
    let âŸ¨t, htf, hstâŸ© := h V hV.1
    âŸ¨t, htf,
      closure_minimal hst <|
        htf.isClosed_biUnion fun _ _ => hV.2.preimage (continuous_id.prod_mk continuous_const)âŸ©
#align totally_bounded.closure TotallyBounded.closure

/-- The image of a totally bounded set under a uniformly continuous map is totally bounded. -/
theorem TotallyBounded.image [UniformSpace Î²] {f : Î± â†’ Î²} {s : Set Î±} (hs : TotallyBounded s)
    (hf : UniformContinuous f) : TotallyBounded (f '' s) := fun t ht =>
  have : { p : Î± Ã— Î± | (f p.1, f p.2) âˆˆ t } âˆˆ ğ“¤ Î± := hf ht
  let âŸ¨c, hfc, hctâŸ© := hs _ this
  âŸ¨f '' c, hfc.image f, by
    simp only [mem_image, iUnion_exists, biUnion_and', iUnion_iUnion_eq_right, image_subset_iff,
      preimage_iUnion, preimage_setOf_eq]
    simp? [subset_def] at hct says
      simp only [mem_setOf_eq, subset_def, mem_iUnion, exists_prop] at hct
    intro x hx; simp
    exact hct x hxâŸ©
#align totally_bounded.image TotallyBounded.image

theorem Ultrafilter.cauchy_of_totallyBounded {s : Set Î±} (f : Ultrafilter Î±) (hs : TotallyBounded s)
    (h : â†‘f â‰¤ ğ“Ÿ s) : Cauchy (f : Filter Î±) :=
  âŸ¨f.neBot', fun _ ht =>
    let âŸ¨t', ht'â‚, ht'_symm, ht'_tâŸ© := comp_symm_of_uniformity ht
    let âŸ¨i, hi, hs_unionâŸ© := hs t' ht'â‚
    have : (â‹ƒ y âˆˆ i, { x | (x, y) âˆˆ t' }) âˆˆ f := mem_of_superset (le_principal_iff.mp h) hs_union
    have : âˆƒ y âˆˆ i, { x | (x, y) âˆˆ t' } âˆˆ f := (Ultrafilter.finite_biUnion_mem_iff hi).1 this
    let âŸ¨y, _, hifâŸ© := this
    have : { x | (x, y) âˆˆ t' } Ã—Ë¢ { x | (x, y) âˆˆ t' } âŠ† compRel t' t' :=
      fun âŸ¨_, _âŸ© âŸ¨(hâ‚ : (_, y) âˆˆ t'), (hâ‚‚ : (_, y) âˆˆ t')âŸ© => âŸ¨y, hâ‚, ht'_symm hâ‚‚âŸ©
    mem_of_superset (prod_mem_prod hif hif) (Subset.trans this ht'_t)âŸ©
#align ultrafilter.cauchy_of_totally_bounded Ultrafilter.cauchy_of_totallyBounded

theorem totallyBounded_iff_filter {s : Set Î±} :
    TotallyBounded s â†” âˆ€ f, NeBot f â†’ f â‰¤ ğ“Ÿ s â†’ âˆƒ c â‰¤ f, Cauchy c := by
  constructor
  Â· exact fun H f hf hfs => âŸ¨Ultrafilter.of f, Ultrafilter.of_le f,
      (Ultrafilter.of f).cauchy_of_totallyBounded H ((Ultrafilter.of_le f).trans hfs)âŸ©
  Â· intro H d hd
    contrapose! H with hd_cover
    set f := â¨… t : Finset Î±, ğ“Ÿ (s \ â‹ƒ y âˆˆ t, { x | (x, y) âˆˆ d })
    have hb : HasAntitoneBasis f fun t : Finset Î± â†¦ s \ â‹ƒ y âˆˆ t, { x | (x, y) âˆˆ d } :=
      .iInf_principal fun _ _ â†¦ diff_subset_diff_right âˆ˜ biUnion_subset_biUnion_left
    have : Filter.NeBot f := hb.1.neBot_iff.2 fun _ â†¦
      nonempty_diff.2 <| hd_cover _ (Finset.finite_toSet _)
    have : f â‰¤ ğ“Ÿ s := iInf_le_of_le âˆ… (by simp)
    refine âŸ¨f, â€¹_â€º, â€¹_â€º, fun c hcf hc => ?_âŸ©
    rcases mem_prod_same_iff.1 (hc.2 hd) with âŸ¨m, hm, hmdâŸ©
    rcases hc.1.nonempty_of_mem hm with âŸ¨y, hymâŸ©
    have : s \ {x | (x, y) âˆˆ d} âˆˆ c := by simpa using hcf (hb.mem {y})
    rcases hc.1.nonempty_of_mem (inter_mem hm this) with âŸ¨z, hzm, -, hyzâŸ©
    exact hyz (hmd âŸ¨hzm, hymâŸ©)
#align totally_bounded_iff_filter totallyBounded_iff_filter

theorem totallyBounded_iff_ultrafilter {s : Set Î±} :
    TotallyBounded s â†” âˆ€ f : Ultrafilter Î±, â†‘f â‰¤ ğ“Ÿ s â†’ Cauchy (f : Filter Î±) := by
  refine âŸ¨fun hs f => f.cauchy_of_totallyBounded hs, fun H => totallyBounded_iff_filter.2 ?_âŸ©
  intro f hf hfs
  exact âŸ¨Ultrafilter.of f, Ultrafilter.of_le f, H _ ((Ultrafilter.of_le f).trans hfs)âŸ©
#align totally_bounded_iff_ultrafilter totallyBounded_iff_ultrafilter

theorem isCompact_iff_totallyBounded_isComplete {s : Set Î±} :
    IsCompact s â†” TotallyBounded s âˆ§ IsComplete s :=
  âŸ¨fun hs =>
    âŸ¨totallyBounded_iff_ultrafilter.2 fun f hf =>
        let âŸ¨_, _, fxâŸ© := isCompact_iff_ultrafilter_le_nhds.1 hs f hf
        cauchy_nhds.mono fx,
      fun f fc fs =>
      let âŸ¨a, as, faâŸ© := @hs f fc.1 fs
      âŸ¨a, as, le_nhds_of_cauchy_adhp fc faâŸ©âŸ©,
    fun âŸ¨ht, hcâŸ© =>
    isCompact_iff_ultrafilter_le_nhds.2 fun f hf =>
      hc _ (totallyBounded_iff_ultrafilter.1 ht f hf) hfâŸ©
#align is_compact_iff_totally_bounded_is_complete isCompact_iff_totallyBounded_isComplete

protected theorem IsCompact.totallyBounded {s : Set Î±} (h : IsCompact s) : TotallyBounded s :=
  (isCompact_iff_totallyBounded_isComplete.1 h).1
#align is_compact.totally_bounded IsCompact.totallyBounded

protected theorem IsCompact.isComplete {s : Set Î±} (h : IsCompact s) : IsComplete s :=
  (isCompact_iff_totallyBounded_isComplete.1 h).2
#align is_compact.is_complete IsCompact.isComplete

-- see Note [lower instance priority]
instance (priority := 100) complete_of_compact {Î± : Type u} [UniformSpace Î±] [CompactSpace Î±] :
    CompleteSpace Î± :=
  âŸ¨fun hf => by simpa using (isCompact_iff_totallyBounded_isComplete.1 isCompact_univ).2 _ hfâŸ©
#align complete_of_compact complete_of_compact

theorem isCompact_of_totallyBounded_isClosed [CompleteSpace Î±] {s : Set Î±} (ht : TotallyBounded s)
    (hc : IsClosed s) : IsCompact s :=
  (@isCompact_iff_totallyBounded_isComplete Î± _ s).2 âŸ¨ht, hc.isCompleteâŸ©
#align is_compact_of_totally_bounded_is_closed isCompact_of_totallyBounded_isClosed

/-- Every Cauchy sequence over `â„•` is totally bounded. -/
theorem CauchySeq.totallyBounded_range {s : â„• â†’ Î±} (hs : CauchySeq s) :
    TotallyBounded (range s) := by
  refine totallyBounded_iff_subset.2 fun a ha => ?_
  cases' cauchySeq_iff.1 hs a ha with n hn
  refine âŸ¨s '' { k | k â‰¤ n }, image_subset_range _ _, (finite_le_nat _).image _, ?_âŸ©
  rw [range_subset_iff, biUnion_image]
  intro m
  rw [mem_iUnionâ‚‚]
  rcases le_total m n with hm | hm
  exacts [âŸ¨m, hm, refl_mem_uniformity haâŸ©, âŸ¨n, le_refl n, hn m hm n le_rflâŸ©]
#align cauchy_seq.totally_bounded_range CauchySeq.totallyBounded_range

/-!
### Sequentially complete space

In this section we prove that a uniform space is complete provided that it is sequentially complete
(i.e., any Cauchy sequence converges) and its uniformity filter admits a countable generating set.
In particular, this applies to (e)metric spaces, see the files `Topology/MetricSpace/EmetricSpace`
and `Topology/MetricSpace/Basic`.

More precisely, we assume that there is a sequence of entourages `U_n` such that any other
entourage includes one of `U_n`. Then any Cauchy filter `f` generates a decreasing sequence of
sets `s_n âˆˆ f` such that `s_n Ã— s_n âŠ† U_n`. Choose a sequence `x_nâˆˆs_n`. It is easy to show
that this is a Cauchy sequence. If this sequence converges to some `a`, then `f â‰¤ ğ“ a`. -/


namespace SequentiallyComplete

variable {f : Filter Î±} (hf : Cauchy f) {U : â„• â†’ Set (Î± Ã— Î±)} (U_mem : âˆ€ n, U n âˆˆ ğ“¤ Î±)
  (U_le : âˆ€ s âˆˆ ğ“¤ Î±, âˆƒ n, U n âŠ† s)

open Set Finset

noncomputable section

/-- An auxiliary sequence of sets approximating a Cauchy filter. -/
def setSeqAux (n : â„•) : { s : Set Î± // s âˆˆ f âˆ§ s Ã—Ë¢ s âŠ† U n } :=
  -- Porting note: changed `âˆƒ _ : s âˆˆ f, ..` to `s âˆˆ f âˆ§ ..`
  Classical.indefiniteDescription _ <| (cauchy_iff.1 hf).2 (U n) (U_mem n)
#align sequentially_complete.set_seq_aux SequentiallyComplete.setSeqAux

/-- Given a Cauchy filter `f` and a sequence `U` of entourages, `set_seq` provides
an antitone sequence of sets `s n âˆˆ f` such that `s n Ã—Ë¢ s n âŠ† U`. -/
def setSeq (n : â„•) : Set Î± :=
  â‹‚ m âˆˆ Set.Iic n, (setSeqAux hf U_mem m).val
#align sequentially_complete.set_seq SequentiallyComplete.setSeq

theorem setSeq_mem (n : â„•) : setSeq hf U_mem n âˆˆ f :=
  (biInter_mem (finite_le_nat n)).2 fun m _ => (setSeqAux hf U_mem m).2.1
#align sequentially_complete.set_seq_mem SequentiallyComplete.setSeq_mem

theorem setSeq_mono â¦ƒm n : â„•â¦„ (h : m â‰¤ n) : setSeq hf U_mem n âŠ† setSeq hf U_mem m :=
  biInter_subset_biInter_left <| Iic_subset_Iic.2 h
#align sequentially_complete.set_seq_mono SequentiallyComplete.setSeq_mono

theorem setSeq_sub_aux (n : â„•) : setSeq hf U_mem n âŠ† setSeqAux hf U_mem n :=
  biInter_subset_of_mem right_mem_Iic
#align sequentially_complete.set_seq_sub_aux SequentiallyComplete.setSeq_sub_aux

theorem setSeq_prod_subset {N m n} (hm : N â‰¤ m) (hn : N â‰¤ n) :
    setSeq hf U_mem m Ã—Ë¢ setSeq hf U_mem n âŠ† U N := fun p hp => by
  refine (setSeqAux hf U_mem N).2.2 âŸ¨?_, ?_âŸ© <;> apply setSeq_sub_aux
  Â· exact setSeq_mono hf U_mem hm hp.1
  Â· exact setSeq_mono hf U_mem hn hp.2
#align sequentially_complete.set_seq_prod_subset SequentiallyComplete.setSeq_prod_subset

/-- A sequence of points such that `seq n âˆˆ setSeq n`. Here `setSeq` is an antitone
sequence of sets `setSeq n âˆˆ f` with diameters controlled by a given sequence
of entourages. -/
def seq (n : â„•) : Î± :=
  (hf.1.nonempty_of_mem (setSeq_mem hf U_mem n)).choose
#align sequentially_complete.seq SequentiallyComplete.seq

theorem seq_mem (n : â„•) : seq hf U_mem n âˆˆ setSeq hf U_mem n :=
  (hf.1.nonempty_of_mem (setSeq_mem hf U_mem n)).choose_spec
#align sequentially_complete.seq_mem SequentiallyComplete.seq_mem

theorem seq_pair_mem â¦ƒN m n : â„•â¦„ (hm : N â‰¤ m) (hn : N â‰¤ n) :
    (seq hf U_mem m, seq hf U_mem n) âˆˆ U N :=
  setSeq_prod_subset hf U_mem hm hn âŸ¨seq_mem hf U_mem m, seq_mem hf U_mem nâŸ©
#align sequentially_complete.seq_pair_mem SequentiallyComplete.seq_pair_mem

theorem seq_is_cauchySeq : CauchySeq <| seq hf U_mem :=
  cauchySeq_of_controlled U U_le <| seq_pair_mem hf U_mem
#align sequentially_complete.seq_is_cauchy_seq SequentiallyComplete.seq_is_cauchySeq

/-- If the sequence `SequentiallyComplete.seq` converges to `a`, then `f â‰¤ ğ“ a`. -/
theorem le_nhds_of_seq_tendsto_nhds â¦ƒa : Î±â¦„ (ha : Tendsto (seq hf U_mem) atTop (ğ“ a)) : f â‰¤ ğ“ a :=
  le_nhds_of_cauchy_adhp_aux
    (fun s hs => by
      rcases U_le s hs with âŸ¨m, hmâŸ©
      rcases tendsto_atTop'.1 ha _ (mem_nhds_left a (U_mem m)) with âŸ¨n, hnâŸ©
      refine
        âŸ¨setSeq hf U_mem (max m n), setSeq_mem hf U_mem _, ?_, seq hf U_mem (max m n), ?_,
          seq_mem hf U_mem _âŸ©
      Â· have := le_max_left m n
        exact Set.Subset.trans (setSeq_prod_subset hf U_mem this this) hm
      Â· exact hm (hn _ <| le_max_right m n))
#align sequentially_complete.le_nhds_of_seq_tendsto_nhds SequentiallyComplete.le_nhds_of_seq_tendsto_nhds

end

end SequentiallyComplete

namespace UniformSpace

open SequentiallyComplete

variable [IsCountablyGenerated (ğ“¤ Î±)]

/-- A uniform space is complete provided that (a) its uniformity filter has a countable basis;
(b) any sequence satisfying a "controlled" version of the Cauchy condition converges. -/
theorem complete_of_convergent_controlled_sequences (U : â„• â†’ Set (Î± Ã— Î±)) (U_mem : âˆ€ n, U n âˆˆ ğ“¤ Î±)
    (HU : âˆ€ u : â„• â†’ Î±, (âˆ€ N m n, N â‰¤ m â†’ N â‰¤ n â†’ (u m, u n) âˆˆ U N) â†’ âˆƒ a, Tendsto u atTop (ğ“ a)) :
    CompleteSpace Î± := by
  obtain âŸ¨U', -, hU'âŸ© := (ğ“¤ Î±).exists_antitone_seq
  have Hmem : âˆ€ n, U n âˆ© U' n âˆˆ ğ“¤ Î± := fun n => inter_mem (U_mem n) (hU'.2 âŸ¨n, Subset.refl _âŸ©)
  refine âŸ¨fun hf => (HU (seq hf Hmem) fun N m n hm hn => ?_).imp <|
    le_nhds_of_seq_tendsto_nhds _ _ fun s hs => ?_âŸ©
  Â· exact inter_subset_left _ _ (seq_pair_mem hf Hmem hm hn)
  Â· rcases hU'.1 hs with âŸ¨N, hNâŸ©
    exact âŸ¨N, Subset.trans (inter_subset_right _ _) hNâŸ©
#align uniform_space.complete_of_convergent_controlled_sequences UniformSpace.complete_of_convergent_controlled_sequences

/-- A sequentially complete uniform space with a countable basis of the uniformity filter is
complete. -/
theorem complete_of_cauchySeq_tendsto (H' : âˆ€ u : â„• â†’ Î±, CauchySeq u â†’ âˆƒ a, Tendsto u atTop (ğ“ a)) :
    CompleteSpace Î± :=
  let âŸ¨U', _, hU'âŸ© := (ğ“¤ Î±).exists_antitone_seq
  complete_of_convergent_controlled_sequences U' (fun n => hU'.2 âŸ¨n, Subset.refl _âŸ©) fun u hu =>
    H' u <| cauchySeq_of_controlled U' (fun _ hs => hU'.1 hs) hu
#align uniform_space.complete_of_cauchy_seq_tendsto UniformSpace.complete_of_cauchySeq_tendsto

variable (Î±)

-- Porting note (#11215): TODO: move to `Topology.UniformSpace.Basic`
instance (priority := 100) firstCountableTopology : FirstCountableTopology Î± :=
  âŸ¨fun a => by rw [nhds_eq_comap_uniformity]; infer_instanceâŸ©
#align uniform_space.first_countable_topology UniformSpace.firstCountableTopology

/-- A separable uniform space with countably generated uniformity filter is second countable:
one obtains a countable basis by taking the balls centered at points in a dense subset,
and with rational "radii" from a countable open symmetric antitone basis of `ğ“¤ Î±`. We do not
register this as an instance, as there is already an instance going in the other direction
from second countable spaces to separable spaces, and we want to avoid loops. -/
theorem secondCountable_of_separable [SeparableSpace Î±] : SecondCountableTopology Î± := by
  rcases exists_countable_dense Î± with âŸ¨s, hsc, hsdâŸ©
  obtain
    âŸ¨t : â„• â†’ Set (Î± Ã— Î±), hto : âˆ€ i : â„•, t i âˆˆ (ğ“¤ Î±).sets âˆ§ IsOpen (t i) âˆ§ SymmetricRel (t i),
      h_basis : (ğ“¤ Î±).HasAntitoneBasis tâŸ© :=
    (@uniformity_hasBasis_open_symmetric Î± _).exists_antitone_subbasis
  choose ht_mem hto hts using hto
  refine âŸ¨âŸ¨â‹ƒ x âˆˆ s, range fun k => ball x (t k), hsc.biUnion fun x _ => countable_range _, ?_âŸ©âŸ©
  refine (isTopologicalBasis_of_isOpen_of_nhds ?_ ?_).eq_generateFrom
  Â· simp only [mem_iUnionâ‚‚, mem_range]
    rintro _ âŸ¨x, _, k, rflâŸ©
    exact isOpen_ball x (hto k)
  Â· intro x V hxV hVo
    simp only [mem_iUnionâ‚‚, mem_range, exists_prop]
    rcases UniformSpace.mem_nhds_iff.1 (IsOpen.mem_nhds hVo hxV) with âŸ¨U, hU, hUVâŸ©
    rcases comp_symm_of_uniformity hU with âŸ¨U', hU', _, hUU'âŸ©
    rcases h_basis.toHasBasis.mem_iff.1 hU' with âŸ¨k, -, hkâŸ©
    rcases hsd.inter_open_nonempty (ball x <| t k) (isOpen_ball x (hto k))
        âŸ¨x, UniformSpace.mem_ball_self _ (ht_mem k)âŸ© with
      âŸ¨y, hxy, hysâŸ©
    refine âŸ¨_, âŸ¨y, hys, k, rflâŸ©, (hts k).subset hxy, fun z hz => ?_âŸ©
    exact hUV (ball_subset_of_comp_subset (hk hxy) hUU' (hk hz))
#align uniform_space.second_countable_of_separable UniformSpace.secondCountable_of_separable

end UniformSpace
