/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro
-/
module

public import Mathlib.Topology.Algebra.Constructions
public import Mathlib.Topology.Bases
public import Mathlib.Algebra.Order.Group.Nat
public import Mathlib.Topology.UniformSpace.DiscreteUniformity

/-!
# Theory of Cauchy filters in uniform spaces. Complete uniform spaces. Totally bounded subsets.
-/

@[expose] public section

universe u v

open Filter Function TopologicalSpace Topology Set UniformSpace Uniformity
open scoped SetRel

variable {Œ± : Type u} {Œ≤ : Type v} [uniformSpace : UniformSpace Œ±]

/-- A filter `f` is Cauchy if for every entourage `r`, there exists an
  `s ‚àà f` such that `s √ó s ‚äÜ r`. This is a generalization of Cauchy
  sequences, because if `a : ‚Ñï ‚Üí Œ±` then the filter of sets containing
  cofinitely many of the `a n` is Cauchy iff `a` is a Cauchy sequence. -/
def Cauchy (f : Filter Œ±) :=
  NeBot f ‚àß f √óÀ¢ f ‚â§ ùì§ Œ±

/-- A set `s` is called *complete*, if any Cauchy filter `f` such that `s ‚àà f`
has a limit in `s` (formally, it satisfies `f ‚â§ ùìù x` for some `x ‚àà s`). -/
def IsComplete (s : Set Œ±) :=
  ‚àÄ f, Cauchy f ‚Üí f ‚â§ ùìü s ‚Üí ‚àÉ x ‚àà s, f ‚â§ ùìù x

theorem Filter.HasBasis.cauchy_iff {Œπ} {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí SetRel Œ± Œ±} (h : (ùì§ Œ±).HasBasis p s)
    {f : Filter Œ±} :
    Cauchy f ‚Üî NeBot f ‚àß ‚àÄ i, p i ‚Üí ‚àÉ t ‚àà f, ‚àÄ x ‚àà t, ‚àÄ y ‚àà t, (x, y) ‚àà s i :=
  and_congr Iff.rfl <|
    (f.basis_sets.prod_self.le_basis_iff h).trans <| by
      simp only [subset_def, Prod.forall, mem_prod_eq, and_imp, id, forall_mem_comm]

theorem cauchy_iff' {f : Filter Œ±} :
    Cauchy f ‚Üî NeBot f ‚àß ‚àÄ s ‚àà ùì§ Œ±, ‚àÉ t ‚àà f, ‚àÄ x ‚àà t, ‚àÄ y ‚àà t, (x, y) ‚àà s :=
  (ùì§ Œ±).basis_sets.cauchy_iff

theorem cauchy_iff {f : Filter Œ±} : Cauchy f ‚Üî NeBot f ‚àß ‚àÄ s ‚àà ùì§ Œ±, ‚àÉ t ‚àà f, t √óÀ¢ t ‚äÜ s :=
  cauchy_iff'.trans <| by
    simp only [subset_def, Prod.forall, mem_prod_eq, and_imp, forall_mem_comm]

lemma cauchy_iff_le {l : Filter Œ±} [hl : l.NeBot] :
    Cauchy l ‚Üî l √óÀ¢ l ‚â§ ùì§ Œ± := by
  simp only [Cauchy, hl, true_and]

theorem Cauchy.ultrafilter_of {l : Filter Œ±} (h : Cauchy l) :
    Cauchy (@Ultrafilter.of _ l h.1 : Filter Œ±) := by
  haveI := h.1
  have := Ultrafilter.of_le l
  exact ‚ü®Ultrafilter.neBot _, (Filter.prod_mono this this).trans h.2‚ü©

theorem cauchy_map_iff {l : Filter Œ≤} {f : Œ≤ ‚Üí Œ±} :
    Cauchy (l.map f) ‚Üî NeBot l ‚àß Tendsto (fun p : Œ≤ √ó Œ≤ => (f p.1, f p.2)) (l √óÀ¢ l) (ùì§ Œ±) := by
  rw [Cauchy, map_neBot_iff, prod_map_map_eq, Tendsto]

theorem cauchy_map_iff' {l : Filter Œ≤} [hl : NeBot l] {f : Œ≤ ‚Üí Œ±} :
    Cauchy (l.map f) ‚Üî Tendsto (fun p : Œ≤ √ó Œ≤ => (f p.1, f p.2)) (l √óÀ¢ l) (ùì§ Œ±) :=
  cauchy_map_iff.trans <| and_iff_right hl

theorem Cauchy.mono {f g : Filter Œ±} [hg : NeBot g] (h_c : Cauchy f) (h_le : g ‚â§ f) : Cauchy g :=
  ‚ü®hg, le_trans (Filter.prod_mono h_le h_le) h_c.right‚ü©

theorem Cauchy.mono' {f g : Filter Œ±} (h_c : Cauchy f) (_ : NeBot g) (h_le : g ‚â§ f) : Cauchy g :=
  h_c.mono h_le

theorem cauchy_nhds {a : Œ±} : Cauchy (ùìù a) :=
  ‚ü®nhds_neBot, nhds_prod_eq.symm.trans_le (nhds_le_uniformity a)‚ü©

theorem cauchy_pure {a : Œ±} : Cauchy (pure a) :=
  cauchy_nhds.mono (pure_le_nhds a)

theorem Filter.Tendsto.cauchy_map {l : Filter Œ≤} [NeBot l] {f : Œ≤ ‚Üí Œ±} {a : Œ±}
    (h : Tendsto f l (ùìù a)) : Cauchy (map f l) :=
  cauchy_nhds.mono h

lemma Cauchy.mono_uniformSpace {u v : UniformSpace Œ≤} {F : Filter Œ≤} (huv : u ‚â§ v)
    (hF : Cauchy (uniformSpace := u) F) : Cauchy (uniformSpace := v) F :=
  ‚ü®hF.1, hF.2.trans huv‚ü©

lemma cauchy_inf_uniformSpace {u v : UniformSpace Œ≤} {F : Filter Œ≤} :
    Cauchy (uniformSpace := u ‚äì v) F ‚Üî
    Cauchy (uniformSpace := u) F ‚àß Cauchy (uniformSpace := v) F := by
  unfold Cauchy
  rw [inf_uniformity (u := u), le_inf_iff, and_and_left]

lemma cauchy_iInf_uniformSpace {Œπ : Sort*} [Nonempty Œπ] {u : Œπ ‚Üí UniformSpace Œ≤}
    {l : Filter Œ≤} :
    Cauchy (uniformSpace := ‚®Ö i, u i) l ‚Üî ‚àÄ i, Cauchy (uniformSpace := u i) l := by
  unfold Cauchy
  rw [iInf_uniformity, le_iInf_iff, forall_and, forall_const]

lemma cauchy_iInf_uniformSpace' {Œπ : Sort*} {u : Œπ ‚Üí UniformSpace Œ≤}
    {l : Filter Œ≤} [l.NeBot] :
    Cauchy (uniformSpace := ‚®Ö i, u i) l ‚Üî ‚àÄ i, Cauchy (uniformSpace := u i) l := by
  simp_rw [cauchy_iff_le (uniformSpace := _), iInf_uniformity, le_iInf_iff]

lemma cauchy_comap_uniformSpace {u : UniformSpace Œ≤} {Œ±} {f : Œ± ‚Üí Œ≤} {l : Filter Œ±} :
    Cauchy (uniformSpace := comap f u) l ‚Üî Cauchy (map f l) := by
  simp only [Cauchy, map_neBot_iff, prod_map_map_eq, map_le_iff_le_comap]
  rfl

lemma cauchy_prod_iff [UniformSpace Œ≤] {F : Filter (Œ± √ó Œ≤)} :
    Cauchy F ‚Üî Cauchy (map Prod.fst F) ‚àß Cauchy (map Prod.snd F) := by
  simp_rw [instUniformSpaceProd, ‚Üê cauchy_comap_uniformSpace, ‚Üê cauchy_inf_uniformSpace]

theorem Cauchy.prod [UniformSpace Œ≤] {f : Filter Œ±} {g : Filter Œ≤} (hf : Cauchy f) (hg : Cauchy g) :
    Cauchy (f √óÀ¢ g) := by
  have := hf.1; have := hg.1
  simpa [cauchy_prod_iff, hf.1] using ‚ü®hf, hg‚ü©

/-- The common part of the proofs of `le_nhds_of_cauchy_adhp` and
`SequentiallyComplete.le_nhds_of_seq_tendsto_nhds`: if for any entourage `s`
one can choose a set `t ‚àà f` of diameter `s` such that it contains a point `y`
with `(x, y) ‚àà s`, then `f` converges to `x`. -/
theorem le_nhds_of_cauchy_adhp_aux {f : Filter Œ±} {x : Œ±}
    (adhs : ‚àÄ s ‚àà ùì§ Œ±, ‚àÉ t ‚àà f, t √óÀ¢ t ‚äÜ s ‚àß ‚àÉ y, (x, y) ‚àà s ‚àß y ‚àà t) : f ‚â§ ùìù x := by
  -- Consider a neighborhood `s` of `x`
  intro s hs
  -- Take an entourage twice smaller than `s`
  rcases comp_mem_uniformity_sets (mem_nhds_uniformity_iff_right.1 hs) with ‚ü®U, U_mem, hU‚ü©
  -- Take a set `t ‚àà f`, `t √ó t ‚äÜ U`, and a point `y ‚àà t` such that `(x, y) ‚àà U`
  rcases adhs U U_mem with ‚ü®t, t_mem, ht, y, hxy, hy‚ü©
  apply mem_of_superset t_mem
  -- Given a point `z ‚àà t`, we have `(x, y) ‚àà U` and `(y, z) ‚àà t √ó t ‚äÜ U`, hence `z ‚àà s`
  exact fun z hz => hU (SetRel.prodMk_mem_comp hxy (ht <| mk_mem_prod hy hz)) rfl

/-- If `x` is an adherent (cluster) point for a Cauchy filter `f`, then it is a limit point
for `f`. -/
theorem le_nhds_of_cauchy_adhp {f : Filter Œ±} {x : Œ±} (hf : Cauchy f) (adhs : ClusterPt x f) :
    f ‚â§ ùìù x :=
  le_nhds_of_cauchy_adhp_aux
    (fun s hs => by
      obtain ‚ü®t, t_mem, ht‚ü© : ‚àÉ t ‚àà f, t √óÀ¢ t ‚äÜ s := (cauchy_iff.1 hf).2 s hs
      use t, t_mem, ht
      exact forall_mem_nonempty_iff_neBot.2 adhs _ (inter_mem_inf (mem_nhds_left x hs) t_mem))

theorem le_nhds_iff_adhp_of_cauchy {f : Filter Œ±} {x : Œ±} (hf : Cauchy f) :
    f ‚â§ ùìù x ‚Üî ClusterPt x f :=
  ‚ü®fun h => ClusterPt.of_le_nhds' h hf.1, le_nhds_of_cauchy_adhp hf‚ü©

nonrec theorem Cauchy.map [UniformSpace Œ≤] {f : Filter Œ±} {m : Œ± ‚Üí Œ≤} (hf : Cauchy f)
    (hm : UniformContinuous m) : Cauchy (map m f) :=
  ‚ü®hf.1.map _,
    calc
      map m f √óÀ¢ map m f = map (Prod.map m m) (f √óÀ¢ f) := Filter.prod_map_map_eq
      _ ‚â§ Filter.map (Prod.map m m) (ùì§ Œ±) := map_mono hf.right
      _ ‚â§ ùì§ Œ≤ := hm‚ü©

nonrec theorem Cauchy.comap [UniformSpace Œ≤] {f : Filter Œ≤} {m : Œ± ‚Üí Œ≤} (hf : Cauchy f)
    (hm : comap (fun p : Œ± √ó Œ± => (m p.1, m p.2)) (ùì§ Œ≤) ‚â§ ùì§ Œ±) [NeBot (comap m f)] :
    Cauchy (comap m f) :=
  ‚ü®‚Äπ_‚Ä∫,
    calc
      comap m f √óÀ¢ comap m f = comap (Prod.map m m) (f √óÀ¢ f) := prod_comap_comap_eq
      _ ‚â§ comap (Prod.map m m) (ùì§ Œ≤) := comap_mono hf.right
      _ ‚â§ ùì§ Œ± := hm‚ü©

theorem Cauchy.comap' [UniformSpace Œ≤] {f : Filter Œ≤} {m : Œ± ‚Üí Œ≤} (hf : Cauchy f)
    (hm : Filter.comap (fun p : Œ± √ó Œ± => (m p.1, m p.2)) (ùì§ Œ≤) ‚â§ ùì§ Œ±)
    (_ : NeBot (Filter.comap m f)) : Cauchy (Filter.comap m f) :=
  hf.comap hm

/-- Cauchy sequences. Usually defined on ‚Ñï, but often it is also useful to say that a function
defined on ‚Ñù is Cauchy at +‚àû to deduce convergence. Therefore, we define it in a type class that
is general enough to cover both ‚Ñï and ‚Ñù, which are the main motivating examples. -/
def CauchySeq [Preorder Œ≤] (u : Œ≤ ‚Üí Œ±) :=
  Cauchy (atTop.map u)

theorem CauchySeq.tendsto_uniformity [Preorder Œ≤] {u : Œ≤ ‚Üí Œ±} (h : CauchySeq u) :
    Tendsto (Prod.map u u) atTop (ùì§ Œ±) := by
  simpa only [Tendsto, prod_map_map_eq', prod_atTop_atTop_eq] using h.right

theorem CauchySeq.nonempty [Preorder Œ≤] {u : Œ≤ ‚Üí Œ±} (hu : CauchySeq u) : Nonempty Œ≤ :=
  @nonempty_of_neBot _ _ <| (map_neBot_iff _).1 hu.1

theorem CauchySeq.mem_entourage {Œ≤ : Type*} [SemilatticeSup Œ≤] {u : Œ≤ ‚Üí Œ±} (h : CauchySeq u)
    {V : SetRel Œ± Œ±} (hV : V ‚àà ùì§ Œ±) : ‚àÉ k‚ÇÄ, ‚àÄ i j, k‚ÇÄ ‚â§ i ‚Üí k‚ÇÄ ‚â§ j ‚Üí (u i, u j) ‚àà V := by
  haveI := h.nonempty
  have := h.tendsto_uniformity; rw [‚Üê prod_atTop_atTop_eq] at this
  simpa [MapsTo] using atTop_basis.prod_self.tendsto_left_iff.1 this V hV

theorem Filter.Tendsto.cauchySeq [SemilatticeSup Œ≤] [Nonempty Œ≤] {f : Œ≤ ‚Üí Œ±} {x}
    (hx : Tendsto f atTop (ùìù x)) : CauchySeq f :=
  hx.cauchy_map

theorem cauchySeq_const [SemilatticeSup Œ≤] [Nonempty Œ≤] (x : Œ±) : CauchySeq fun _ : Œ≤ => x :=
  tendsto_const_nhds.cauchySeq

theorem cauchySeq_iff_tendsto [Nonempty Œ≤] [SemilatticeSup Œ≤] {u : Œ≤ ‚Üí Œ±} :
    CauchySeq u ‚Üî Tendsto (Prod.map u u) atTop (ùì§ Œ±) :=
  cauchy_map_iff'.trans <| by simp only [prod_atTop_atTop_eq, Prod.map_def]

theorem CauchySeq.comp_tendsto {Œ≥} [Preorder Œ≤] [SemilatticeSup Œ≥] [Nonempty Œ≥] {f : Œ≤ ‚Üí Œ±}
    (hf : CauchySeq f) {g : Œ≥ ‚Üí Œ≤} (hg : Tendsto g atTop atTop) : CauchySeq (f ‚àò g) :=
  ‚ü®inferInstance, le_trans (prod_le_prod.mpr ‚ü®Tendsto.comp le_rfl hg, Tendsto.comp le_rfl hg‚ü©) hf.2‚ü©

theorem CauchySeq.comp_injective [SemilatticeSup Œ≤] [NoMaxOrder Œ≤] [Nonempty Œ≤] {u : ‚Ñï ‚Üí Œ±}
    (hu : CauchySeq u) {f : Œ≤ ‚Üí ‚Ñï} (hf : Injective f) : CauchySeq (u ‚àò f) :=
  hu.comp_tendsto <| Nat.cofinite_eq_atTop ‚ñ∏ hf.tendsto_cofinite.mono_left atTop_le_cofinite

theorem Function.Bijective.cauchySeq_comp_iff {f : ‚Ñï ‚Üí ‚Ñï} (hf : Bijective f) (u : ‚Ñï ‚Üí Œ±) :
    CauchySeq (u ‚àò f) ‚Üî CauchySeq u := by
  refine ‚ü®fun H => ?_, fun H => H.comp_injective hf.injective‚ü©
  lift f to ‚Ñï ‚âÉ ‚Ñï using hf
  simpa only [Function.comp_def, f.apply_symm_apply] using H.comp_injective f.symm.injective

theorem CauchySeq.subseq_subseq_mem {V : ‚Ñï ‚Üí SetRel Œ± Œ±} (hV : ‚àÄ n, V n ‚àà ùì§ Œ±) {u : ‚Ñï ‚Üí Œ±}
    (hu : CauchySeq u) {f g : ‚Ñï ‚Üí ‚Ñï} (hf : Tendsto f atTop atTop) (hg : Tendsto g atTop atTop) :
    ‚àÉ œÜ : ‚Ñï ‚Üí ‚Ñï, StrictMono œÜ ‚àß ‚àÄ n, ((u ‚àò f ‚àò œÜ) n, (u ‚àò g ‚àò œÜ) n) ‚àà V n := by
  rw [cauchySeq_iff_tendsto] at hu
  exact ((hu.comp <| hf.prod_atTop hg).comp tendsto_atTop_diagonal).subseq_mem hV

-- todo: generalize this and other lemmas to a nonempty semilattice
theorem cauchySeq_iff' {u : ‚Ñï ‚Üí Œ±} :
    CauchySeq u ‚Üî ‚àÄ V ‚àà ùì§ Œ±, ‚àÄ·∂† k in atTop, k ‚àà Prod.map u u ‚Åª¬π' V :=
  cauchySeq_iff_tendsto

theorem cauchySeq_iff {u : ‚Ñï ‚Üí Œ±} :
    CauchySeq u ‚Üî ‚àÄ V ‚àà ùì§ Œ±, ‚àÉ N, ‚àÄ k ‚â• N, ‚àÄ l ‚â• N, (u k, u l) ‚àà V := by
  simp only [cauchySeq_iff', Filter.eventually_atTop_prod_self', mem_preimage, Prod.map_apply]

theorem CauchySeq.prodMap {Œ≥ Œ¥} [UniformSpace Œ≤] [Preorder Œ≥] [Preorder Œ¥] {u : Œ≥ ‚Üí Œ±} {v : Œ¥ ‚Üí Œ≤}
    (hu : CauchySeq u) (hv : CauchySeq v) : CauchySeq (Prod.map u v) := by
  simpa only [CauchySeq, prod_map_map_eq', prod_atTop_atTop_eq] using hu.prod hv

theorem CauchySeq.prodMk {Œ≥} [UniformSpace Œ≤] [Preorder Œ≥] {u : Œ≥ ‚Üí Œ±} {v : Œ≥ ‚Üí Œ≤}
    (hu : CauchySeq u) (hv : CauchySeq v) : CauchySeq fun x => (u x, v x) :=
  haveI := hu.1.of_map
  (Cauchy.prod hu hv).mono (tendsto_map.prodMk tendsto_map)

theorem CauchySeq.eventually_eventually [Preorder Œ≤] {u : Œ≤ ‚Üí Œ±} (hu : CauchySeq u)
    {V : SetRel Œ± Œ±} (hV : V ‚àà ùì§ Œ±) : ‚àÄ·∂† k in atTop, ‚àÄ·∂† l in atTop, (u k, u l) ‚àà V :=
  eventually_atTop_curry <| hu.tendsto_uniformity hV

theorem UniformContinuous.comp_cauchySeq {Œ≥} [UniformSpace Œ≤] [Preorder Œ≥] {f : Œ± ‚Üí Œ≤}
    (hf : UniformContinuous f) {u : Œ≥ ‚Üí Œ±} (hu : CauchySeq u) : CauchySeq (f ‚àò u) :=
  hu.map hf

theorem CauchySeq.subseq_mem {V : ‚Ñï ‚Üí SetRel Œ± Œ±} (hV : ‚àÄ n, V n ‚àà ùì§ Œ±) {u : ‚Ñï ‚Üí Œ±}
    (hu : CauchySeq u) : ‚àÉ œÜ : ‚Ñï ‚Üí ‚Ñï, StrictMono œÜ ‚àß ‚àÄ n, (u <| œÜ (n + 1), u <| œÜ n) ‚àà V n := by
  have : ‚àÄ n, ‚àÉ N, ‚àÄ k ‚â• N, ‚àÄ l ‚â• k, (u l, u k) ‚àà V n := fun n => by
    rw [cauchySeq_iff] at hu
    rcases hu _ (hV n) with ‚ü®N, H‚ü©
    exact ‚ü®N, fun k hk l hl => H _ (le_trans hk hl) _ hk‚ü©
  obtain ‚ü®œÜ : ‚Ñï ‚Üí ‚Ñï, œÜ_extr : StrictMono œÜ, hœÜ : ‚àÄ n, ‚àÄ l ‚â• œÜ n, (u l, u <| œÜ n) ‚àà V n‚ü© :=
    extraction_forall_of_eventually' this
  exact ‚ü®œÜ, œÜ_extr, fun n => hœÜ _ _ (œÜ_extr <| Nat.lt_add_one n).le‚ü©

theorem Filter.Tendsto.subseq_mem_entourage {V : ‚Ñï ‚Üí SetRel Œ± Œ±} (hV : ‚àÄ n, V n ‚àà ùì§ Œ±) {u : ‚Ñï ‚Üí Œ±}
    {a : Œ±} (hu : Tendsto u atTop (ùìù a)) : ‚àÉ œÜ : ‚Ñï ‚Üí ‚Ñï, StrictMono œÜ ‚àß (u (œÜ 0), a) ‚àà V 0 ‚àß
      ‚àÄ n, (u <| œÜ (n + 1), u <| œÜ n) ‚àà V (n + 1) := by
  rcases mem_atTop_sets.1 (hu (ball_mem_nhds a (symm_le_uniformity <| hV 0))) with ‚ü®n, hn‚ü©
  rcases (hu.comp (tendsto_add_atTop_nat n)).cauchySeq.subseq_mem fun n => hV (n + 1) with
    ‚ü®œÜ, œÜ_mono, hœÜV‚ü©
  exact ‚ü®fun k => œÜ k + n, œÜ_mono.add_const _, hn _ le_add_self, hœÜV‚ü©

/-- If a Cauchy sequence has a convergent subsequence, then it converges. -/
theorem tendsto_nhds_of_cauchySeq_of_subseq [Preorder Œ≤] {u : Œ≤ ‚Üí Œ±} (hu : CauchySeq u)
    {Œπ : Type*} {f : Œπ ‚Üí Œ≤} {p : Filter Œπ} [NeBot p] (hf : Tendsto f p atTop) {a : Œ±}
    (ha : Tendsto (u ‚àò f) p (ùìù a)) : Tendsto u atTop (ùìù a) :=
  le_nhds_of_cauchy_adhp hu (ha.mapClusterPt.of_comp hf)

/-- Any shift of a Cauchy sequence is also a Cauchy sequence. -/
theorem cauchySeq_shift {u : ‚Ñï ‚Üí Œ±} (k : ‚Ñï) : CauchySeq (fun n ‚Ü¶ u (n + k)) ‚Üî CauchySeq u := by
  constructor <;> intro h
  ¬∑ rw [cauchySeq_iff] at h ‚ä¢
    intro V mV
    obtain ‚ü®N, h‚ü© := h V mV
    use N + k
    intro a ha b hb
    convert h (a - k) (Nat.le_sub_of_add_le ha) (b - k) (Nat.le_sub_of_add_le hb) <;> lia
  ¬∑ exact h.comp_tendsto (tendsto_add_atTop_nat k)

theorem Filter.HasBasis.cauchySeq_iff {Œ≥} [Nonempty Œ≤] [SemilatticeSup Œ≤] {u : Œ≤ ‚Üí Œ±} {p : Œ≥ ‚Üí Prop}
    {s : Œ≥ ‚Üí SetRel Œ± Œ±} (h : (ùì§ Œ±).HasBasis p s) :
    CauchySeq u ‚Üî ‚àÄ i, p i ‚Üí ‚àÉ N, ‚àÄ m, N ‚â§ m ‚Üí ‚àÄ n, N ‚â§ n ‚Üí (u m, u n) ‚àà s i := by
  rw [cauchySeq_iff_tendsto, ‚Üê prod_atTop_atTop_eq]
  refine (atTop_basis.prod_self.tendsto_iff h).trans ?_
  simp only [true_and, Prod.forall, mem_prod_eq,
    mem_Ici, and_imp, Prod.map, @forall_swap (_ ‚â§ _) Œ≤]

theorem Filter.HasBasis.cauchySeq_iff' {Œ≥} [Nonempty Œ≤] [SemilatticeSup Œ≤] {u : Œ≤ ‚Üí Œ±}
    {p : Œ≥ ‚Üí Prop} {s : Œ≥ ‚Üí SetRel Œ± Œ±} (H : (ùì§ Œ±).HasBasis p s) :
    CauchySeq u ‚Üî ‚àÄ i, p i ‚Üí ‚àÉ N, ‚àÄ n ‚â• N, (u n, u N) ‚àà s i := by
  refine H.cauchySeq_iff.trans ‚ü®fun h i hi => ?_, fun h i hi => ?_‚ü©
  ¬∑ exact (h i hi).imp fun N hN n hn => hN n hn N le_rfl
  ¬∑ rcases comp_symm_of_uniformity (H.mem_of_mem hi) with ‚ü®t, ht, ht', hts‚ü©
    rcases H.mem_iff.1 ht with ‚ü®j, hj, hjt‚ü©
    refine (h j hj).imp fun N hN m hm n hn => hts ‚ü®u N, hjt ?_, ht' <| hjt ?_‚ü©
    exacts [hN m hm, hN n hn]

theorem cauchySeq_of_controlled [SemilatticeSup Œ≤] [Nonempty Œ≤] (U : Œ≤ ‚Üí SetRel Œ± Œ±)
    (hU : ‚àÄ s ‚àà ùì§ Œ±, ‚àÉ n, U n ‚äÜ s) {f : Œ≤ ‚Üí Œ±}
    (hf : ‚àÄ ‚¶ÉN m n : Œ≤‚¶Ñ, N ‚â§ m ‚Üí N ‚â§ n ‚Üí (f m, f n) ‚àà U N) : CauchySeq f :=
  cauchySeq_iff_tendsto.2
    (by
      intro s hs
      rw [mem_map, mem_atTop_sets]
      obtain ‚ü®N, hN‚ü© := hU s hs
      refine ‚ü®(N, N), fun mn hmn => ?_‚ü©
      obtain ‚ü®m, n‚ü© := mn
      exact hN (hf hmn.1 hmn.2))

theorem isComplete_iff_clusterPt {s : Set Œ±} :
    IsComplete s ‚Üî ‚àÄ l, Cauchy l ‚Üí l ‚â§ ùìü s ‚Üí ‚àÉ x ‚àà s, ClusterPt x l :=
  forall‚ÇÉ_congr fun _ hl _ => exists_congr fun _ => and_congr_right fun _ =>
    le_nhds_iff_adhp_of_cauchy hl

theorem isComplete_iff_ultrafilter {s : Set Œ±} :
    IsComplete s ‚Üî ‚àÄ l : Ultrafilter Œ±, Cauchy (l : Filter Œ±) ‚Üí ‚Üël ‚â§ ùìü s ‚Üí ‚àÉ x ‚àà s, ‚Üël ‚â§ ùìù x := by
  refine ‚ü®fun h l => h l, fun H => isComplete_iff_clusterPt.2 fun l hl hls => ?_‚ü©
  haveI := hl.1
  rcases H (Ultrafilter.of l) hl.ultrafilter_of ((Ultrafilter.of_le l).trans hls) with ‚ü®x, hxs, hxl‚ü©
  exact ‚ü®x, hxs, (ClusterPt.of_le_nhds hxl).mono (Ultrafilter.of_le l)‚ü©

theorem isComplete_iff_ultrafilter' {s : Set Œ±} :
    IsComplete s ‚Üî ‚àÄ l : Ultrafilter Œ±, Cauchy (l : Filter Œ±) ‚Üí s ‚àà l ‚Üí ‚àÉ x ‚àà s, ‚Üël ‚â§ ùìù x :=
  isComplete_iff_ultrafilter.trans <| by simp only [le_principal_iff, Ultrafilter.mem_coe]

protected theorem IsComplete.union {s t : Set Œ±} (hs : IsComplete s) (ht : IsComplete t) :
    IsComplete (s ‚à™ t) := by
  simp only [isComplete_iff_ultrafilter', Ultrafilter.union_mem_iff, or_imp] at *
  exact fun l hl =>
    ‚ü®fun hsl => (hs l hl hsl).imp fun x hx => ‚ü®Or.inl hx.1, hx.2‚ü©, fun htl =>
      (ht l hl htl).imp fun x hx => ‚ü®Or.inr hx.1, hx.2‚ü©‚ü©

theorem isComplete_iUnion_separated {Œπ : Sort*} {s : Œπ ‚Üí Set Œ±} (hs : ‚àÄ i, IsComplete (s i))
    {U : SetRel Œ± Œ±} (hU : U ‚àà ùì§ Œ±) (hd : ‚àÄ (i j : Œπ), ‚àÄ x ‚àà s i, ‚àÄ y ‚àà s j, (x, y) ‚àà U ‚Üí i = j) :
    IsComplete (‚ãÉ i, s i) := by
  set S := ‚ãÉ i, s i
  intro l hl hls
  rw [le_principal_iff] at hls
  obtain ‚ü®hl_ne, hl'‚ü© := cauchy_iff.1 hl
  obtain ‚ü®t, htS, htl, htU‚ü© : ‚àÉ t, t ‚äÜ S ‚àß t ‚àà l ‚àß t √óÀ¢ t ‚äÜ U := by
    rcases hl' U hU with ‚ü®t, htl, htU‚ü©
    refine ‚ü®t ‚à© S, inter_subset_right, inter_mem htl hls, Subset.trans ?_ htU‚ü©
    gcongr <;> apply inter_subset_left
  obtain ‚ü®i, hi‚ü© : ‚àÉ i, t ‚äÜ s i := by
    rcases Filter.nonempty_of_mem htl with ‚ü®x, hx‚ü©
    rcases mem_iUnion.1 (htS hx) with ‚ü®i, hi‚ü©
    refine ‚ü®i, fun y hy => ?_‚ü©
    rcases mem_iUnion.1 (htS hy) with ‚ü®j, hj‚ü©
    rwa [hd i j x hi y hj (htU <| mk_mem_prod hx hy)]
  rcases hs i l hl (le_principal_iff.2 <| mem_of_superset htl hi) with ‚ü®x, hxs, hlx‚ü©
  exact ‚ü®x, mem_iUnion.2 ‚ü®i, hxs‚ü©, hlx‚ü©

/-- A complete space is defined here using uniformities. A uniform space
  is complete if every Cauchy filter converges. -/
class CompleteSpace (Œ± : Type u) [UniformSpace Œ±] : Prop where
  /-- In a complete uniform space, every Cauchy filter converges. -/
  complete : ‚àÄ {f : Filter Œ±}, Cauchy f ‚Üí ‚àÉ x, f ‚â§ ùìù x

theorem complete_univ {Œ± : Type u} [UniformSpace Œ±] [CompleteSpace Œ±] :
    IsComplete (univ : Set Œ±) := fun f hf _ => by
  rcases CompleteSpace.complete hf with ‚ü®x, hx‚ü©
  exact ‚ü®x, mem_univ x, hx‚ü©

instance CompleteSpace.prod [UniformSpace Œ≤] [CompleteSpace Œ±] [CompleteSpace Œ≤] :
    CompleteSpace (Œ± √ó Œ≤) where
  complete hf :=
    let ‚ü®x1, hx1‚ü© := CompleteSpace.complete <| hf.map uniformContinuous_fst
    let ‚ü®x2, hx2‚ü© := CompleteSpace.complete <| hf.map uniformContinuous_snd
    ‚ü®(x1, x2), by rw [nhds_prod_eq, le_prod]; constructor <;> assumption‚ü©

lemma CompleteSpace.fst_of_prod [UniformSpace Œ≤] [CompleteSpace (Œ± √ó Œ≤)] [h : Nonempty Œ≤] :
    CompleteSpace Œ± where
  complete hf :=
    let ‚ü®y‚ü© := h
    let ‚ü®(a, b), hab‚ü© := CompleteSpace.complete <| hf.prod <| cauchy_pure (a := y)
    ‚ü®a, by simpa only [map_fst_prod, nhds_prod_eq] using map_mono (m := Prod.fst) hab‚ü©

lemma CompleteSpace.snd_of_prod [UniformSpace Œ≤] [CompleteSpace (Œ± √ó Œ≤)] [h : Nonempty Œ±] :
    CompleteSpace Œ≤ where
  complete hf :=
    let ‚ü®x‚ü© := h
    let ‚ü®(a, b), hab‚ü© := CompleteSpace.complete <| (cauchy_pure (a := x)).prod hf
    ‚ü®b, by simpa only [map_snd_prod, nhds_prod_eq] using map_mono (m := Prod.snd) hab‚ü©

lemma completeSpace_prod_of_nonempty [UniformSpace Œ≤] [Nonempty Œ±] [Nonempty Œ≤] :
    CompleteSpace (Œ± √ó Œ≤) ‚Üî CompleteSpace Œ± ‚àß CompleteSpace Œ≤ :=
  ‚ü®fun _ ‚Ü¶ ‚ü®.fst_of_prod (Œ≤ := Œ≤), .snd_of_prod (Œ± := Œ±)‚ü©, fun ‚ü®_, _‚ü© ‚Ü¶ .prod‚ü©

@[to_additive]
instance CompleteSpace.mulOpposite [CompleteSpace Œ±] : CompleteSpace Œ±·µê·µí·µñ where
  complete hf :=
    MulOpposite.op_surjective.exists.mpr <|
      let ‚ü®x, hx‚ü© := CompleteSpace.complete (hf.map MulOpposite.uniformContinuous_unop)
      ‚ü®x, (map_le_iff_le_comap.mp hx).trans_eq <| MulOpposite.comap_unop_nhds _‚ü©

/-- If `univ` is complete, the space is a complete space -/
theorem completeSpace_of_isComplete_univ (h : IsComplete (univ : Set Œ±)) : CompleteSpace Œ± :=
  ‚ü®fun hf => let ‚ü®x, _, hx‚ü© := h _ hf ((@principal_univ Œ±).symm ‚ñ∏ le_top); ‚ü®x, hx‚ü©‚ü©

theorem completeSpace_iff_isComplete_univ : CompleteSpace Œ± ‚Üî IsComplete (univ : Set Œ±) :=
  ‚ü®@complete_univ Œ± _, completeSpace_of_isComplete_univ‚ü©

theorem completeSpace_iff_ultrafilter :
    CompleteSpace Œ± ‚Üî ‚àÄ l : Ultrafilter Œ±, Cauchy (l : Filter Œ±) ‚Üí ‚àÉ x : Œ±, ‚Üël ‚â§ ùìù x := by
  simp [completeSpace_iff_isComplete_univ, isComplete_iff_ultrafilter]

theorem cauchy_iff_exists_le_nhds [CompleteSpace Œ±] {l : Filter Œ±} [NeBot l] :
    Cauchy l ‚Üî ‚àÉ x, l ‚â§ ùìù x :=
  ‚ü®CompleteSpace.complete, fun ‚ü®_, hx‚ü© => cauchy_nhds.mono hx‚ü©

theorem cauchy_map_iff_exists_tendsto [CompleteSpace Œ±] {l : Filter Œ≤} {f : Œ≤ ‚Üí Œ±} [NeBot l] :
    Cauchy (l.map f) ‚Üî ‚àÉ x, Tendsto f l (ùìù x) :=
  cauchy_iff_exists_le_nhds

/-- A Cauchy sequence in a complete space converges -/
theorem cauchySeq_tendsto_of_complete [Preorder Œ≤] [CompleteSpace Œ±] {u : Œ≤ ‚Üí Œ±}
    (H : CauchySeq u) : ‚àÉ x, Tendsto u atTop (ùìù x) :=
  CompleteSpace.complete H

/-- If `K` is a complete subset, then any Cauchy sequence in `K` converges to a point in `K` -/
theorem cauchySeq_tendsto_of_isComplete [Preorder Œ≤] {K : Set Œ±} (h‚ÇÅ : IsComplete K)
    {u : Œ≤ ‚Üí Œ±} (h‚ÇÇ : ‚àÄ n, u n ‚àà K) (h‚ÇÉ : CauchySeq u) : ‚àÉ v ‚àà K, Tendsto u atTop (ùìù v) :=
  h‚ÇÅ _ h‚ÇÉ <| le_principal_iff.2 <| mem_map_iff_exists_image.2
    ‚ü®univ, univ_mem, by rwa [image_univ, range_subset_iff]‚ü©

theorem Cauchy.le_nhds_lim [CompleteSpace Œ±] {f : Filter Œ±} (hf : Cauchy f) :
    haveI := hf.1.nonempty; f ‚â§ ùìù (lim f) :=
  _root_.le_nhds_lim (CompleteSpace.complete hf)

theorem CauchySeq.tendsto_limUnder [Preorder Œ≤] [CompleteSpace Œ±] {u : Œ≤ ‚Üí Œ±} (h : CauchySeq u) :
    haveI := h.1.nonempty; Tendsto u atTop (ùìù <| limUnder atTop u) :=
  h.le_nhds_lim

theorem IsClosed.isComplete [CompleteSpace Œ±] {s : Set Œ±} (h : IsClosed s) : IsComplete s :=
  fun _ cf fs =>
  let ‚ü®x, hx‚ü© := CompleteSpace.complete cf
  ‚ü®x, isClosed_iff_clusterPt.mp h x (cf.left.mono (le_inf hx fs)), hx‚ü©

namespace DiscreteUniformity

variable [DiscreteUniformity Œ±]

/-- A Cauchy filter in a discrete uniform space is contained in the principal filter
of a point. -/
theorem eq_pure_of_cauchy {f : Filter Œ±} (hf : Cauchy f) : ‚àÉ x : Œ±, f = pure x := by
  rcases hf with ‚ü®f_ne_bot, f_le‚ü©
  simp only [DiscreteUniformity.eq_principal_setRelId, le_principal_iff, mem_prod_iff] at f_le
  obtain ‚ü®S, hS, T, hT, H‚ü© := f_le
  obtain ‚ü®x, rfl, _, _, _‚ü© := SetRel.exists_eq_singleton_of_prod_subset_id
    (f_ne_bot.nonempty_of_mem hS) (f_ne_bot.nonempty_of_mem hT) H
  exact ‚ü®x, f_ne_bot.le_pure_iff.mp <| le_pure_iff.mpr hS‚ü©

/-- The discrete uniformity makes a space complete. -/
instance : CompleteSpace Œ± where
  complete {f} hf := by
    obtain ‚ü®x, rfl‚ü© := eq_pure_of_cauchy hf
    exact ‚ü®x, pure_le_nhds x‚ü©

variable {X}

/-- A constant to which a Cauchy filter in a discrete uniform space converges. -/
noncomputable def cauchyConst {f : Filter Œ±} (hf : Cauchy f) : Œ± :=
  (eq_pure_of_cauchy hf).choose

theorem eq_pure_cauchyConst {f : Filter Œ±} (hf : Cauchy f) : f = pure (cauchyConst hf) :=
  (eq_pure_of_cauchy hf).choose_spec

end DiscreteUniformity

/-- A set `s` is totally bounded if for every entourage `d` there is a finite
  set of points `t` such that every element of `s` is `d`-near to some element of `t`. -/
def TotallyBounded (s : Set Œ±) : Prop :=
  ‚àÄ d ‚àà ùì§ Œ±, ‚àÉ t : Set Œ±, t.Finite ‚àß s ‚äÜ ‚ãÉ y ‚àà t, { x | (x, y) ‚àà d }

/-- A filter `f` is totally bounded if for every entourage `d`, the `d`-neighborhood of some finite
set is in `f`. -/
protected def Filter.TotallyBounded (f : Filter Œ±) :=
  ‚àÄ d : SetRel Œ± Œ±, d ‚àà ùì§ Œ± ‚Üí ‚àÉ t : Set Œ±, t.Finite ‚àß d.preimage t ‚àà f

theorem Filter.totallyBounded_principal_iff {s : Set Œ±} :
    (ùìü s).TotallyBounded ‚Üî TotallyBounded s := by
  simp only [Filter.TotallyBounded, mem_principal, SetRel.preimage_eq_biUnion, TotallyBounded]

theorem Filter.TotallyBounded.exists_subset_of_mem {f : Filter Œ±} (hf : f.TotallyBounded)
    {s : Set Œ±} (hs : s ‚àà f) {U : SetRel Œ± Œ±} (hU : U ‚àà ùì§ Œ±) :
    ‚àÉ t ‚äÜ s, Set.Finite t ‚àß U.preimage t ‚àà f := by
  rcases comp_symm_of_uniformity hU with ‚ü®r, hr, rs, rU‚ü©
  rcases hf r hr with ‚ü®k, fk, ks‚ü©
  let u := k ‚à© { y | ‚àÉ x ‚àà s, (x, y) ‚àà r }
  choose g hgs hgr using fun x : u => x.coe_prop.2
  refine ‚ü®range g, ?_, ?_, ?_‚ü©
  ¬∑ exact range_subset_iff.2 hgs
  ¬∑ haveI : Fintype u := (fk.inter_of_left _).fintype
    exact finite_range g
  ¬∑ filter_upwards [hs, ks] with x xs ‚ü®y, hy, xy‚ü©
    simp_rw [SetRel.preimage, exists_range_iff]
    set z : ‚Ü•u := ‚ü®y, hy, ‚ü®x, xs, xy‚ü©‚ü©
    exact ‚ü®z, rU ‚ü®y, xy, rs (hgr z)‚ü©‚ü©

theorem TotallyBounded.exists_subset {s : Set Œ±} (hs : TotallyBounded s) {U : SetRel Œ± Œ±}
    (hU : U ‚àà ùì§ Œ±) : ‚àÉ t, t ‚äÜ s ‚àß Set.Finite t ‚àß s ‚äÜ ‚ãÉ y ‚àà t, { x | (x, y) ‚àà U } := by
  rw [‚Üê Filter.totallyBounded_principal_iff] at hs
  simp_rw [‚Üê SetRel.preimage_eq_biUnion]
  exact hs.exists_subset_of_mem (Filter.mem_principal_self s) hU

theorem totallyBounded_iff_subset {s : Set Œ±} :
    TotallyBounded s ‚Üî
      ‚àÄ d ‚àà ùì§ Œ±, ‚àÉ t, t ‚äÜ s ‚àß Set.Finite t ‚àß s ‚äÜ ‚ãÉ y ‚àà t, { x | (x, y) ‚àà d } :=
  ‚ü®fun H _ hd ‚Ü¶ H.exists_subset hd, fun H d hd ‚Ü¶ let ‚ü®t, _, ht‚ü© := H d hd; ‚ü®t, ht‚ü©‚ü©

theorem Filter.HasBasis.totallyBounded_iff {Œπ} {p : Œπ ‚Üí Prop} {U : Œπ ‚Üí SetRel Œ± Œ±}
    (H : (ùì§ Œ±).HasBasis p U) {s : Set Œ±} :
    TotallyBounded s ‚Üî ‚àÄ i, p i ‚Üí ‚àÉ t : Set Œ±, Set.Finite t ‚àß s ‚äÜ ‚ãÉ y ‚àà t, { x | (x, y) ‚àà U i } :=
  H.forall_iff fun _ _ hUV h =>
    h.imp fun _ ht => ‚ü®ht.1, ht.2.trans <| iUnion‚ÇÇ_mono fun _ _ _ hy => hUV hy‚ü©

theorem Filter.HasBasis.filter_totallyBounded_iff {Œπ} {p : Œπ ‚Üí Prop} {U : Œπ ‚Üí SetRel Œ± Œ±}
    (H : (ùì§ Œ±).HasBasis p U) {f : Filter Œ±} :
    f.TotallyBounded ‚Üî ‚àÄ i, p i ‚Üí ‚àÉ t : Set Œ±, Set.Finite t ‚àß (U i).preimage t ‚àà f :=
  H.forall_iff fun _ _ _ h =>
    h.imp fun _ ht => ‚ü®ht.1, f.mem_of_superset ht.2 <| by gcongr‚ü©

theorem totallyBounded_of_forall_isSymm {s : Set Œ±}
    (h : ‚àÄ V ‚àà ùì§ Œ±, SetRel.IsSymm V ‚Üí ‚àÉ t : Set Œ±, Set.Finite t ‚àß s ‚äÜ ‚ãÉ y ‚àà t, ball y V) :
    TotallyBounded s :=
  UniformSpace.hasBasis_symmetric.totallyBounded_iff.2 fun V ‚ü®_, _‚ü© => by
    simpa only [ball_eq_of_symmetry] using h V ‚Äπ_‚Ä∫ ‚Äπ_‚Ä∫

theorem TotallyBounded.subset {s‚ÇÅ s‚ÇÇ : Set Œ±} (hs : s‚ÇÅ ‚äÜ s‚ÇÇ) (h : TotallyBounded s‚ÇÇ) :
    TotallyBounded s‚ÇÅ := fun d hd =>
  let ‚ü®t, ht‚ÇÅ, ht‚ÇÇ‚ü© := h d hd
  ‚ü®t, ht‚ÇÅ, Subset.trans hs ht‚ÇÇ‚ü©

theorem Filter.TotallyBounded.mono {f g : Filter Œ±} (h : f ‚â§ g) (hg : g.TotallyBounded) :
    f.TotallyBounded :=
  fun U hU => (hg U hU).imp fun _ => And.imp_right (@h _)

theorem Filter.TotallyBounded.totallyBounded_setOf_clusterPt {f : Filter Œ±} (h : f.TotallyBounded) :
    TotallyBounded {x | ClusterPt x f} := by
  refine uniformity_hasBasis_closed.totallyBounded_iff.2 fun V hV => ?_
  obtain ‚ü®t, htf, hst‚ü© := h V hV.1
  refine ‚ü®t, htf, fun x hx => ?_‚ü©
  rw [‚Üê SetRel.preimage_eq_biUnion, id, ‚Üê (hV.2.relPreimage_of_finite htf).closure_eq]
  exact hx.mem_closure_of_mem _ hst

/-- The closure of a totally bounded set is totally bounded. -/
theorem TotallyBounded.closure {s : Set Œ±} (h : TotallyBounded s) : TotallyBounded (closure s) := by
  rw [closure_eq_cluster_pts]
  exact (Filter.totallyBounded_principal_iff.mpr h).totallyBounded_setOf_clusterPt

@[simp]
lemma totallyBounded_closure {s : Set Œ±} : TotallyBounded (closure s) ‚Üî TotallyBounded s :=
  ‚ü®fun h ‚Ü¶ h.subset subset_closure, TotallyBounded.closure‚ü©

@[simp]
lemma Filter.totallyBounded_iSup {Œπ : Sort*} [Finite Œπ] {f : Œπ ‚Üí Filter Œ±} :
    (‚®Ü i, f i).TotallyBounded ‚Üî ‚àÄ i, (f i).TotallyBounded := by
  refine ‚ü®fun h i ‚Ü¶ h.mono (le_iSup _ _), fun h U hU ‚Ü¶ ?_‚ü©
  choose t htf ht using (h ¬∑ U hU)
  refine ‚ü®‚ãÉ i, t i, finite_iUnion htf, ?_‚ü©
  simp_rw [U.preimage_iUnion, ‚Üê le_principal_iff, ‚Üê iSup_principal] at ht ‚ä¢
  gcongr; apply ht

lemma Filter.totallyBounded_biSup {Œπ : Type*} {I : Set Œπ} (hI : I.Finite) {f : Œπ ‚Üí Filter Œ±} :
    (‚®Ü i ‚àà I, f i).TotallyBounded ‚Üî ‚àÄ i ‚àà I, (f i).TotallyBounded := by
  have := hI.to_subtype
  rw [iSup_subtype', totallyBounded_iSup, Subtype.forall]

lemma totallyBounded_sSup {S : Set (Filter Œ±)} (hS : S.Finite) :
    (sSup S).TotallyBounded ‚Üî ‚àÄ f ‚àà S, f.TotallyBounded := by
  rw [sSup_eq_iSup, totallyBounded_biSup hS]

/-- A finite indexed union is totally bounded
if and only if each set of the family is totally bounded. -/
@[simp]
lemma totallyBounded_iUnion {Œπ : Sort*} [Finite Œπ] {s : Œπ ‚Üí Set Œ±} :
    TotallyBounded (‚ãÉ i, s i) ‚Üî ‚àÄ i, TotallyBounded (s i) := by
  simp_rw [‚Üê Filter.totallyBounded_principal_iff, ‚Üê Filter.iSup_principal,
    Filter.totallyBounded_iSup]

/-- A union indexed by a finite set is totally bounded
if and only if each set of the family is totally bounded. -/
lemma totallyBounded_biUnion {Œπ : Type*} {I : Set Œπ} (hI : I.Finite) {s : Œπ ‚Üí Set Œ±} :
    TotallyBounded (‚ãÉ i ‚àà I, s i) ‚Üî ‚àÄ i ‚àà I, TotallyBounded (s i) := by
  have := hI.to_subtype
  rw [biUnion_eq_iUnion, totallyBounded_iUnion, Subtype.forall]

/-- A union of a finite family of sets is totally bounded
if and only if each set of the family is totally bounded. -/
lemma totallyBounded_sUnion {S : Set (Set Œ±)} (hS : S.Finite) :
    TotallyBounded (‚ãÉ‚ÇÄ S) ‚Üî ‚àÄ s ‚àà S, TotallyBounded s := by
  rw [sUnion_eq_biUnion, totallyBounded_biUnion hS]

/-- A finite set is totally bounded. -/
lemma Set.Finite.totallyBounded {s : Set Œ±} (hs : s.Finite) : TotallyBounded s := fun _U hU ‚Ü¶
  ‚ü®s, hs, fun _x hx ‚Ü¶ mem_biUnion hx <| refl_mem_uniformity hU‚ü©

/-- A subsingleton is totally bounded. -/
lemma Set.Subsingleton.totallyBounded {s : Set Œ±} (hs : s.Subsingleton) :
    TotallyBounded s :=
  hs.finite.totallyBounded

@[simp]
lemma totallyBounded_singleton (a : Œ±) : TotallyBounded {a} := (finite_singleton a).totallyBounded

@[simp]
theorem totallyBounded_empty : TotallyBounded (‚àÖ : Set Œ±) := finite_empty.totallyBounded

@[simp]
theorem Filter.totallyBounded_bot : (‚ä• : Filter Œ±).TotallyBounded := by
  rw [‚Üê principal_empty, totallyBounded_principal_iff]
  exact totallyBounded_empty

/-- The union of two sets is totally bounded
if and only if each of the two sets is totally bounded. -/
@[simp]
lemma totallyBounded_union {s t : Set Œ±} :
    TotallyBounded (s ‚à™ t) ‚Üî TotallyBounded s ‚àß TotallyBounded t := by
  rw [union_eq_iUnion, totallyBounded_iUnion]
  simp [and_comm]

/-- The union of two totally bounded sets is totally bounded. -/
protected lemma TotallyBounded.union {s t : Set Œ±} (hs : TotallyBounded s) (ht : TotallyBounded t) :
    TotallyBounded (s ‚à™ t) :=
  totallyBounded_union.2 ‚ü®hs, ht‚ü©

@[simp]
lemma totallyBounded_insert (a : Œ±) {s : Set Œ±} :
    TotallyBounded (insert a s) ‚Üî TotallyBounded s := by
  simp_rw [‚Üê singleton_union, totallyBounded_union, totallyBounded_singleton, true_and]

protected alias ‚ü®_, TotallyBounded.insert‚ü© := totallyBounded_insert

@[simp]
lemma Filter.totallyBounded_sup {f g : Filter Œ±} :
    (f ‚äî g).TotallyBounded ‚Üî f.TotallyBounded ‚àß g.TotallyBounded := by
  rw [sup_eq_iSup, totallyBounded_iSup]
  simp [and_comm]

lemma Filter.TotallyBounded.sup {f g : Filter Œ±} (hf : f.TotallyBounded) (hg : g.TotallyBounded) :
    (f ‚äî g).TotallyBounded :=
  totallyBounded_sup.2 ‚ü®hf, hg‚ü©

theorem Filter.TotallyBounded.map [UniformSpace Œ≤] {f : Œ± ‚Üí Œ≤} {g : Filter Œ±}
    (hg : g.TotallyBounded) (hf : UniformContinuous f) : (g.map f).TotallyBounded := fun t ht =>
  let ‚ü®c, hfc, hct‚ü© := hg _ (hf ht)
  ‚ü®f '' c, hfc.image f, by simpa [SetRel.preimage]‚ü©

/-- The image of a totally bounded set under a uniformly continuous map is totally bounded. -/
theorem TotallyBounded.image [UniformSpace Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ±} (hs : TotallyBounded s)
    (hf : UniformContinuous f) : TotallyBounded (f '' s) := by
  simp only [‚Üê Filter.totallyBounded_principal_iff, ‚Üê Filter.map_principal] at hs ‚ä¢
  exact hs.map hf

theorem Ultrafilter.cauchy_of_totallyBounded' (f : Ultrafilter Œ±) (hf : f.TotallyBounded) :
    Cauchy (f : Filter Œ±) :=
  ‚ü®f.neBot', fun _ ht =>
    let ‚ü®t', ht'‚ÇÅ, ht'_symm, ht'_t‚ü© := comp_symm_of_uniformity ht
    let ‚ü®i, hi, ht'_f‚ü© := hf t' ht'‚ÇÅ
    have : ‚àÉ y ‚àà i, { x | (x, y) ‚àà t' } ‚àà f := (Ultrafilter.eventually_exists_mem_iff hi).1 ht'_f
    let ‚ü®y, _, hif‚ü© := this
    have : {x | (x, y) ‚àà t'} √óÀ¢ {x | (x, y) ‚àà t'} ‚äÜ t' ‚óã t' :=
      fun ‚ü®_, _‚ü© ‚ü®(h‚ÇÅ : (_, y) ‚àà t'), (h‚ÇÇ : (_, y) ‚àà t')‚ü© => ‚ü®y, h‚ÇÅ, ht'_symm h‚ÇÇ‚ü©
    mem_of_superset (prod_mem_prod hif hif) (Subset.trans this ht'_t)‚ü©

theorem Ultrafilter.cauchy_of_totallyBounded {s : Set Œ±} (f : Ultrafilter Œ±) (hs : TotallyBounded s)
    (h : ‚Üëf ‚â§ ùìü s) : Cauchy (f : Filter Œ±) :=
  f.cauchy_of_totallyBounded' <| (Filter.totallyBounded_principal_iff.mpr hs).mono h

protected theorem Filter.totallyBounded_iff_filter {g : Filter Œ±} :
    g.TotallyBounded ‚Üî ‚àÄ f, NeBot f ‚Üí f ‚â§ g ‚Üí ‚àÉ c ‚â§ f, Cauchy c := by
  constructor
  ¬∑ exact fun H f hf hfs => ‚ü®Ultrafilter.of f, Ultrafilter.of_le f,
      (Ultrafilter.of f).cauchy_of_totallyBounded' (H.mono ((Ultrafilter.of_le f).trans hfs))‚ü©
  ¬∑ intro H d hd
    contrapose! H with hd_cover
    set f := ‚®Ö t : Finset Œ±, g ‚äì ùìü (d.preimage t)·∂ú
    have hb : Antitone fun t : Finset Œ± ‚Ü¶ g ‚äì ùìü (d.preimage t)·∂ú :=
      fun s t (h : s ‚äÜ t) => by beta_reduce; gcongr
    have : Filter.NeBot f :=
      (Filter.iInf_neBot_iff_of_directed' <| hb.directed_ge).mpr fun t =>
        Filter.notMem_iff_inf_principal_compl.mp <| hd_cover t t.finite_toSet
    have : f ‚â§ g := iInf_le_of_le ‚àÖ (by simp)
    refine ‚ü®f, ‚Äπ_‚Ä∫, ‚Äπ_‚Ä∫, fun c hcf hc => ?_‚ü©
    rcases mem_prod_same_iff.1 (hc.2 hd) with ‚ü®m, hm, hmd‚ü©
    rcases hc.1.nonempty_of_mem hm with ‚ü®y, hym‚ü©
    have : {x | (x, y) ‚àà d}·∂ú ‚àà c := by
      simpa [SetRel.preimage] using hcf.trans <| (iInf_le _ {y}).trans inf_le_right
    rcases hc.1.nonempty_of_mem (inter_mem hm this) with ‚ü®z, hzm, hyz‚ü©
    exact hyz (hmd ‚ü®hzm, hym‚ü©)

protected theorem Filter.totallyBounded_iff_ultrafilter {g : Filter Œ±} :
    g.TotallyBounded ‚Üî ‚àÄ f : Ultrafilter Œ±, ‚Üëf ‚â§ g ‚Üí Cauchy (f : Filter Œ±) := by
  refine ‚ü®fun hg f hf => f.cauchy_of_totallyBounded' <| hg.mono hf,
    fun H => g.totallyBounded_iff_filter.2 ?_‚ü©
  intro f hf hfs
  exact ‚ü®Ultrafilter.of f, Ultrafilter.of_le f, H _ ((Ultrafilter.of_le f).trans hfs)‚ü©

theorem totallyBounded_iff_filter {s : Set Œ±} :
    TotallyBounded s ‚Üî ‚àÄ f, NeBot f ‚Üí f ‚â§ ùìü s ‚Üí ‚àÉ c ‚â§ f, Cauchy c := by
  rw [‚Üê Filter.totallyBounded_principal_iff, Filter.totallyBounded_iff_filter]

theorem totallyBounded_iff_ultrafilter {s : Set Œ±} :
    TotallyBounded s ‚Üî ‚àÄ f : Ultrafilter Œ±, ‚Üëf ‚â§ ùìü s ‚Üí Cauchy (f : Filter Œ±) := by
  rw [‚Üê Filter.totallyBounded_principal_iff, Filter.totallyBounded_iff_ultrafilter]

theorem isCompact_iff_totallyBounded_isComplete {s : Set Œ±} :
    IsCompact s ‚Üî TotallyBounded s ‚àß IsComplete s :=
  ‚ü®fun hs =>
    ‚ü®totallyBounded_iff_ultrafilter.2 fun f hf =>
        let ‚ü®_, _, fx‚ü© := isCompact_iff_ultrafilter_le_nhds.1 hs f hf
        cauchy_nhds.mono fx,
      fun f fc fs =>
      let ‚ü®a, as, fa‚ü© := @hs f fc.1 fs
      ‚ü®a, as, le_nhds_of_cauchy_adhp fc fa‚ü©‚ü©,
    fun ‚ü®ht, hc‚ü© =>
    isCompact_iff_ultrafilter_le_nhds.2 fun f hf =>
      hc _ (totallyBounded_iff_ultrafilter.1 ht f hf) hf‚ü©

protected theorem IsCompact.totallyBounded {s : Set Œ±} (h : IsCompact s) : TotallyBounded s :=
  (isCompact_iff_totallyBounded_isComplete.1 h).1

protected theorem IsCompact.isComplete {s : Set Œ±} (h : IsCompact s) : IsComplete s :=
  (isCompact_iff_totallyBounded_isComplete.1 h).2

-- see Note [lower instance priority]
instance (priority := 100) complete_of_compact {Œ± : Type u} [UniformSpace Œ±] [CompactSpace Œ±] :
    CompleteSpace Œ± :=
  ‚ü®fun hf => by simpa using (isCompact_iff_totallyBounded_isComplete.1 isCompact_univ).2 _ hf‚ü©

theorem TotallyBounded.isCompact_of_isComplete {s : Set Œ±} (ht : TotallyBounded s)
    (hc : IsComplete s) : IsCompact s := isCompact_iff_totallyBounded_isComplete.mpr ‚ü®ht, hc‚ü©

theorem TotallyBounded.isCompact_of_isClosed [CompleteSpace Œ±] {s : Set Œ±} (ht : TotallyBounded s)
    (hc : IsClosed s) : IsCompact s := ht.isCompact_of_isComplete hc.isComplete

@[deprecated (since := "2025-08-30")] alias isCompact_of_totallyBounded_isClosed :=
    TotallyBounded.isCompact_of_isClosed

theorem Filter.TotallyBounded.isCompact_setOf_clusterPt
    [CompleteSpace Œ±] {f : Filter Œ±} (hf : f.TotallyBounded) : IsCompact {x | ClusterPt x f} :=
  hf.totallyBounded_setOf_clusterPt.isCompact_of_isClosed isClosed_setOf_clusterPt

theorem Filter.TotallyBounded.exists_clusterPt
    [CompleteSpace Œ±] {f : Filter Œ±} [f.NeBot] (hf : f.TotallyBounded) : ‚àÉ x, ClusterPt x f := by
  let m := Ultrafilter.of f
  have hmf : m ‚â§ f := Ultrafilter.of_le f
  have hm := m.cauchy_of_totallyBounded' (hf.mono hmf)
  obtain ‚ü®x, hx‚ü© := CompleteSpace.complete hm
  rw [le_nhds_iff_adhp_of_cauchy hm] at hx
  exact ‚ü®x, hx.mono hmf‚ü©

/-- Every Cauchy sequence over `‚Ñï` is totally bounded. -/
theorem CauchySeq.totallyBounded_range {s : ‚Ñï ‚Üí Œ±} (hs : CauchySeq s) :
    TotallyBounded (range s) := by
  intro a ha
  obtain ‚ü®n, hn‚ü© := cauchySeq_iff.1 hs a ha
  refine ‚ü®s '' { k | k ‚â§ n }, (finite_le_nat _).image _, ?_‚ü©
  rw [range_subset_iff, biUnion_image]
  intro m
  rw [mem_iUnion‚ÇÇ]
  rcases le_total m n with hm | hm
  exacts [‚ü®m, hm, refl_mem_uniformity ha‚ü©, ‚ü®n, le_refl n, hn m hm n le_rfl‚ü©]

/-- Given a family of points `xs n`, a family of entourages `V n` of the diagonal and a family of
natural numbers `u n`, the intersection over `n` of the `V n`-neighborhood of `xs 1, ..., xs (u n)`.
Designed to be relatively compact when `V n` tends to the diagonal. -/
def interUnionBalls (xs : ‚Ñï ‚Üí Œ±) (u : ‚Ñï ‚Üí ‚Ñï) (V : ‚Ñï ‚Üí SetRel Œ± Œ±) : Set Œ± :=
  ‚ãÇ n, ‚ãÉ m ‚â§ u n, UniformSpace.ball (xs m) (Prod.swap ‚Åª¬π' V n)

lemma totallyBounded_interUnionBalls {p : ‚Ñï ‚Üí Prop} {U : ‚Ñï ‚Üí SetRel Œ± Œ±}
    (H : (uniformity Œ±).HasBasis p U) (xs : ‚Ñï ‚Üí Œ±) (u : ‚Ñï ‚Üí ‚Ñï) :
    TotallyBounded (interUnionBalls xs u U) := by
  rw [Filter.HasBasis.totallyBounded_iff H]
  intro i _
  have h_subset : interUnionBalls xs u U
      ‚äÜ ‚ãÉ m ‚â§ u i, UniformSpace.ball (xs m) (Prod.swap ‚Åª¬π' U i) :=
    fun x hx ‚Ü¶ Set.mem_iInter.1 hx i
  classical
  refine ‚ü®Finset.image xs (Finset.range (u i + 1)), Finset.finite_toSet _, fun x hx ‚Ü¶ ?_‚ü©
  simp only [Finset.coe_image, Finset.coe_range, mem_image, mem_Iio, iUnion_exists, biUnion_and',
    iUnion_iUnion_eq_right, Nat.lt_succ_iff]
  exact h_subset hx

/-- The construction `interUnionBalls` is used to have a relatively compact set. -/
theorem isCompact_closure_interUnionBalls {p : ‚Ñï ‚Üí Prop} {U : ‚Ñï ‚Üí SetRel Œ± Œ±}
    (H : (uniformity Œ±).HasBasis p U) [CompleteSpace Œ±] (xs : ‚Ñï ‚Üí Œ±) (u : ‚Ñï ‚Üí ‚Ñï) :
    IsCompact (closure (interUnionBalls xs u U)) := by
  rw [isCompact_iff_totallyBounded_isComplete]
  refine ‚ü®TotallyBounded.closure ?_, isClosed_closure.isComplete‚ü©
  exact totallyBounded_interUnionBalls H xs u

/-!
### Sequentially complete space

In this section we prove that a uniform space is complete provided that it is sequentially complete
(i.e., any Cauchy sequence converges) and its uniformity filter admits a countable generating set.
In particular, this applies to (e)metric spaces, see the files `Topology/MetricSpace/EmetricSpace`
and `Topology/MetricSpace/Basic`.

More precisely, we assume that there is a sequence of entourages `U_n` such that any other
entourage includes one of `U_n`. Then any Cauchy filter `f` generates a decreasing sequence of
sets `s_n ‚àà f` such that `s_n √ó s_n ‚äÜ U_n`. Choose a sequence `x_n‚ààs_n`. It is easy to show
that this is a Cauchy sequence. If this sequence converges to some `a`, then `f ‚â§ ùìù a`. -/


namespace SequentiallyComplete

variable {f : Filter Œ±} (hf : Cauchy f) {U : ‚Ñï ‚Üí SetRel Œ± Œ±} (U_mem : ‚àÄ n, U n ‚àà ùì§ Œ±)

open Set Finset

noncomputable section

/-- An auxiliary sequence of sets approximating a Cauchy filter. -/
def setSeqAux (n : ‚Ñï) : { s : Set Œ± // s ‚àà f ‚àß s √óÀ¢ s ‚äÜ U n } :=
  Classical.indefiniteDescription _ <| (cauchy_iff.1 hf).2 (U n) (U_mem n)

/-- Given a Cauchy filter `f` and a sequence `U` of entourages, `set_seq` provides
an antitone sequence of sets `s n ‚àà f` such that `s n √óÀ¢ s n ‚äÜ U`. -/
def setSeq (n : ‚Ñï) : Set Œ± :=
  ‚ãÇ m ‚àà Set.Iic n, (setSeqAux hf U_mem m).val

theorem setSeq_mem (n : ‚Ñï) : setSeq hf U_mem n ‚àà f :=
  (biInter_mem (finite_le_nat n)).2 fun m _ => (setSeqAux hf U_mem m).2.1

theorem setSeq_mono ‚¶Ém n : ‚Ñï‚¶Ñ (h : m ‚â§ n) : setSeq hf U_mem n ‚äÜ setSeq hf U_mem m :=
  biInter_subset_biInter_left <| Iic_subset_Iic.2 h

theorem setSeq_sub_aux (n : ‚Ñï) : setSeq hf U_mem n ‚äÜ setSeqAux hf U_mem n :=
  biInter_subset_of_mem self_mem_Iic

theorem setSeq_prod_subset {N m n} (hm : N ‚â§ m) (hn : N ‚â§ n) :
    setSeq hf U_mem m √óÀ¢ setSeq hf U_mem n ‚äÜ U N := fun p hp => by
  refine (setSeqAux hf U_mem N).2.2 ‚ü®?_, ?_‚ü© <;> apply setSeq_sub_aux
  ¬∑ exact setSeq_mono hf U_mem hm hp.1
  ¬∑ exact setSeq_mono hf U_mem hn hp.2

/-- A sequence of points such that `seq n ‚àà setSeq n`. Here `setSeq` is an antitone
sequence of sets `setSeq n ‚àà f` with diameters controlled by a given sequence
of entourages. -/
def seq (n : ‚Ñï) : Œ± :=
  (hf.1.nonempty_of_mem (setSeq_mem hf U_mem n)).choose

theorem seq_mem (n : ‚Ñï) : seq hf U_mem n ‚àà setSeq hf U_mem n :=
  (hf.1.nonempty_of_mem (setSeq_mem hf U_mem n)).choose_spec

theorem seq_pair_mem ‚¶ÉN m n : ‚Ñï‚¶Ñ (hm : N ‚â§ m) (hn : N ‚â§ n) :
    (seq hf U_mem m, seq hf U_mem n) ‚àà U N :=
  setSeq_prod_subset hf U_mem hm hn ‚ü®seq_mem hf U_mem m, seq_mem hf U_mem n‚ü©

theorem seq_is_cauchySeq (U_le : ‚àÄ s ‚àà ùì§ Œ±, ‚àÉ n, U n ‚äÜ s) : CauchySeq <| seq hf U_mem :=
  cauchySeq_of_controlled U U_le <| seq_pair_mem hf U_mem

/-- If the sequence `SequentiallyComplete.seq` converges to `a`, then `f ‚â§ ùìù a`. -/
theorem le_nhds_of_seq_tendsto_nhds (U_le : ‚àÄ s ‚àà ùì§ Œ±, ‚àÉ n, U n ‚äÜ s)
    ‚¶Éa : Œ±‚¶Ñ (ha : Tendsto (seq hf U_mem) atTop (ùìù a)) : f ‚â§ ùìù a :=
  le_nhds_of_cauchy_adhp_aux
    (fun s hs => by
      rcases U_le s hs with ‚ü®m, hm‚ü©
      rcases tendsto_atTop'.1 ha _ (mem_nhds_left a (U_mem m)) with ‚ü®n, hn‚ü©
      refine
        ‚ü®setSeq hf U_mem (max m n), setSeq_mem hf U_mem _, ?_, seq hf U_mem (max m n), ?_,
          seq_mem hf U_mem _‚ü©
      ¬∑ have := le_max_left m n
        exact Set.Subset.trans (setSeq_prod_subset hf U_mem this this) hm
      ¬∑ exact hm (hn _ <| le_max_right m n))

end

end SequentiallyComplete

namespace UniformSpace

open SequentiallyComplete

variable [IsCountablyGenerated (ùì§ Œ±)]

/-- A uniform space is complete provided that (a) its uniformity filter has a countable basis;
(b) any sequence satisfying a "controlled" version of the Cauchy condition converges. -/
theorem complete_of_convergent_controlled_sequences (U : ‚Ñï ‚Üí SetRel Œ± Œ±) (U_mem : ‚àÄ n, U n ‚àà ùì§ Œ±)
    (HU : ‚àÄ u : ‚Ñï ‚Üí Œ±, (‚àÄ N m n, N ‚â§ m ‚Üí N ‚â§ n ‚Üí (u m, u n) ‚àà U N) ‚Üí ‚àÉ a, Tendsto u atTop (ùìù a)) :
    CompleteSpace Œ± := by
  obtain ‚ü®U', -, hU'‚ü© := (ùì§ Œ±).exists_antitone_seq
  have Hmem : ‚àÄ n, U n ‚à© U' n ‚àà ùì§ Œ± := fun n => inter_mem (U_mem n) (hU'.2 ‚ü®n, Subset.refl _‚ü©)
  refine ‚ü®fun hf => (HU (seq hf Hmem) fun N m n hm hn => ?_).imp <|
    le_nhds_of_seq_tendsto_nhds _ _ fun s hs => ?_‚ü©
  ¬∑ exact inter_subset_left (seq_pair_mem hf Hmem hm hn)
  ¬∑ rcases hU'.1 hs with ‚ü®N, hN‚ü©
    exact ‚ü®N, Subset.trans inter_subset_right hN‚ü©

/-- A sequentially complete uniform space with a countable basis of the uniformity filter is
complete. -/
theorem complete_of_cauchySeq_tendsto (H' : ‚àÄ u : ‚Ñï ‚Üí Œ±, CauchySeq u ‚Üí ‚àÉ a, Tendsto u atTop (ùìù a)) :
    CompleteSpace Œ± :=
  let ‚ü®U', _, hU'‚ü© := (ùì§ Œ±).exists_antitone_seq
  complete_of_convergent_controlled_sequences U' (fun n => hU'.2 ‚ü®n, Subset.refl _‚ü©) fun u hu =>
    H' u <| cauchySeq_of_controlled U' (fun _ hs => hU'.1 hs) hu

variable (Œ±)

-- TODO: move to `Topology.UniformSpace.Basic`
instance (priority := 100) firstCountableTopology : FirstCountableTopology Œ± :=
  ‚ü®fun a => by rw [nhds_eq_comap_uniformity]; infer_instance‚ü©

/-- A separable uniform space with countably generated uniformity filter is second countable:
one obtains a countable basis by taking the balls centered at points in a dense subset,
and with rational "radii" from a countable open symmetric antitone basis of `ùì§ Œ±`. -/
instance secondCountable_of_separable [SeparableSpace Œ±] : SecondCountableTopology Œ± := by
  rcases exists_countable_dense Œ± with ‚ü®s, hsc, hsd‚ü©
  obtain
    ‚ü®t : ‚Ñï ‚Üí SetRel Œ± Œ±, hto : ‚àÄ i : ‚Ñï, t i ‚àà (ùì§ Œ±).sets ‚àß IsOpen (t i) ‚àß (t i).IsSymm,
      h_basis : (ùì§ Œ±).HasAntitoneBasis t‚ü© :=
    (@uniformity_hasBasis_open_symmetric Œ± _).exists_antitone_subbasis
  choose ht_mem hto hts using hto
  refine ‚ü®‚ü®‚ãÉ x ‚àà s, range fun k => ball x (t k), hsc.biUnion fun x _ => countable_range _, ?_‚ü©‚ü©
  refine (isTopologicalBasis_of_isOpen_of_nhds ?_ ?_).eq_generateFrom
  ¬∑ simp only [mem_iUnion‚ÇÇ, mem_range]
    rintro _ ‚ü®x, _, k, rfl‚ü©
    exact isOpen_ball x (hto k)
  ¬∑ intro x V hxV hVo
    simp only [mem_iUnion‚ÇÇ, mem_range, exists_prop]
    rcases UniformSpace.mem_nhds_iff.1 (IsOpen.mem_nhds hVo hxV) with ‚ü®U, hU, hUV‚ü©
    rcases comp_symm_of_uniformity hU with ‚ü®U', hU', _, hUU'‚ü©
    rcases h_basis.toHasBasis.mem_iff.1 hU' with ‚ü®k, -, hk‚ü©
    rcases hsd.inter_open_nonempty (ball x <| t k) (isOpen_ball x (hto k))
        ‚ü®x, UniformSpace.mem_ball_self _ (ht_mem k)‚ü© with
      ‚ü®y, hxy, hys‚ü©
    refine ‚ü®_, ‚ü®y, hys, k, rfl‚ü©, (t k).symm hxy, fun z hz => ?_‚ü©
    exact hUV (ball_subset_of_comp_subset (hk hxy) hUU' (hk hz))

variable {Œ±}

theorem subset_countable_closure_of_almost_dense_set (s : Set Œ±)
    (hs : ‚àÄ U ‚àà ùì§ Œ±, ‚àÉ t : Set Œ±, t.Countable ‚àß s ‚äÜ ‚ãÉ x ‚àà t, ball x U) :
    ‚àÉ t, t ‚äÜ s ‚àß t.Countable ‚àß s ‚äÜ closure t := by
  obtain ‚ü®B, hB, _‚ü© := has_seq_basis Œ±
  replace hs (n : ‚Ñï) := hs (B n) (hB.mem n)
  choose t tC ht using hs
  have := fun n => (tC n).to_subtype
  choose o hox hos using fun (n : ‚Ñï) (x : t n) (hx : (ball x.1 (B n) ‚à© s).Nonempty) => hx
  refine ‚ü®‚ãÉ (n) (x), range (o n x), iUnion‚ÇÇ_subset fun _ _ => range_subset_iff.2 (hos _ _),
    countable_iUnion fun _ => countable_iUnion fun _ => countable_range _, fun x hx => ?_‚ü©
  rw [mem_closure_iff_ball]
  intro U hU
  obtain ‚ü®V, hV, hVU‚ü© := comp_mem_uniformity_sets hU
  obtain ‚ü®n, hn‚ü© := hB.mem_iff.1 hV
  specialize ht n hx
  rw [mem_iUnion‚ÇÇ] at ht
  obtain ‚ü®y, hy, hyx‚ü© := ht
  refine ‚ü®o n ‚ü®y, hy‚ü© ‚ü®x, hyx, hx‚ü©, ?_, ?_‚ü©
  ¬∑ apply ball_mono ((SetRel.comp_subset_comp hn hn).trans hVU)
    exact mem_ball_comp (mem_ball_symmetry.2 hyx) (hox n ‚ü®y, hy‚ü© ‚ü®x, hyx, hx‚ü©)
  ¬∑ exact mem_iUnion‚ÇÇ_of_mem ‚ü®y, hy‚ü© (mem_range_self ‚ü®x, hyx, hx‚ü©)

theorem secondCountable_of_almost_dense_set
    (hs : ‚àÄ U ‚àà ùì§ Œ±, ‚àÉ t : Set Œ±, t.Countable ‚àß ‚ãÉ x ‚àà t, ball x U = univ) :
    SecondCountableTopology Œ± := by
  suffices SeparableSpace Œ± from UniformSpace.secondCountable_of_separable Œ±
  have : ‚àÄ U ‚àà ùì§ Œ±, ‚àÉ t : Set Œ±, Set.Countable t ‚àß univ ‚äÜ ‚ãÉ x ‚àà t, ball x U := by
    simpa only [univ_subset_iff] using hs
  rcases subset_countable_closure_of_almost_dense_set (univ : Set Œ±) this with ‚ü®t, -, htc, ht‚ü©
  exact ‚ü®‚ü®t, htc, fun x => ht (mem_univ x)‚ü©‚ü©

/-- A totally bounded set is separable in countably generated uniform spaces. This can be obtained
from the more general `UniformSpace.subset_countable_closure_of_almost_dense_set`. -/
lemma _root_.TotallyBounded.isSeparable {s : Set Œ±} (h : TotallyBounded s) :
    TopologicalSpace.IsSeparable s := by
  obtain ‚ü®t, -, htc, hts‚ü© := subset_countable_closure_of_almost_dense_set s fun U hU => by
    obtain ‚ü®t, ht, hst‚ü© := h (SetRel.inv U)
      (mem_of_superset (symmetrize_mem_uniformity hU) SetRel.symmetrize_subset_inv)
    exact ‚ü®t, ht.countable, hst‚ü©
  exact ‚ü®t, htc, hts‚ü©

end UniformSpace
