/-
Copyright (c) 2024 Emilie Burgun. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Emilie Burgun
-/

import Mathlib.Topology.CompletelyRegular
import Mathlib.Topology.Order.Lattice
import Mathlib.Data.Fintype.Order

/-!
# Uniform spaces are completely regular

This module proves that uniform spaces are equivalent to completely regular spaces,
which are spaces in which each closed set `K ‚äÜ X` and point `x ‚àà K·∂ú`
admits a continuous function `f` from `X` to the unit interval,
such that `f x = 0` and `‚àÄ y ‚àà K, f y = 1`.

To obtain this result, we first show that for any point of open set `x ‚àà s`,
there exists a family `D` of pseudometrics and an `Œµ > 0`,
such that `‚ãÇ m ‚àà D, @Metric.ball _ m x Œµ ‚äÜ s`.

One can then use this to define a continuous function `f` satisfying the requirements of a
completely regular space.

## Main statements

* `UniformSpace.UniformSpaceOf`, the uniform space associated with a set `s ‚àà ùì§ X`,
  whose uniformity is coarser than `ùì§ X` and that is countably generated.
* `UniformSpace.PseudoMetricSpaceOf`, the pseudometric space associated with its corresponding
  `UniformSpaceOf` space.
* `UniformSpace.exists_pseudoMetricSpace_family_of_isOpen`: for every point `x ‚àà s` with `s` open,
  there exists a finite family of pseudometric spaces coarser than `X` and an `Œµ > 0`, such that
  the intersection of those families is a subset of `s`.
* `UniformSpace.completelyRegularSpace`: a uniform space is a completely regular space.

## TODO

Provide a way to construct a uniformity from the definition of a completely regular spaces,
fully deprecating `CompletelyRegularSpace`.

## References

* N. Bourbaki, General Topology, Chapter 9
* Stephen Willard, General Topology

-/

variable {X : Type*} [unf : UniformSpace X]
open Topology Uniformity

namespace UniformSpace

section PseudoMetrics

variable {s : Set (X √ó X)}

/--
A series of symmetric sets in `X √ó X`, with `UniformityBasisOf _ n ‚äÜ s` for some `s ‚àà ùì§ X`,
and `(UniformityBasisOf _ (n + 1)) ^ 2 ‚äÜ UniformityBasisOf _ n`.

When used to generate a filter, this series yields a uniformity (`UniformSpace.UniformSpaceOf`)
that is coarser than the ambient `ùì§ X`.
-/
noncomputable def UniformityBasisOf (s_in_unf : s ‚àà ùì§ X) :
    ‚Ñï ‚Üí { s : Set (X √ó X) // s ‚àà ùì§ X }
  | 0 => ‚ü®symmetrizeRel s, symmetrize_mem_uniformity s_in_unf‚ü©
  | Nat.succ n =>
    let ‚ü®_, t_in_uni‚ü© := UniformSpace.UniformityBasisOf s_in_unf n
    ‚ü®(comp_symm_mem_uniformity_sets t_in_uni).choose,
      (comp_symm_mem_uniformity_sets t_in_uni).choose_spec.1‚ü©

theorem uniformityBasisOf_symmetric (s_in_unf : s ‚àà ùì§ X) (n : ‚Ñï) :
    SymmetricRel (UniformityBasisOf s_in_unf n).val := by
  cases n with
  | zero => exact symmetric_symmetrizeRel s
  | succ n =>
    exact (comp_symm_mem_uniformity_sets <| (UniformityBasisOf s_in_unf n).prop).choose_spec.2.1

theorem uniformityBasisOf_comp_subset (s_in_unf : s ‚àà ùì§ X) (n : ‚Ñï) :
    compRel
      (UniformityBasisOf s_in_unf <| n + 1).val
      (UniformityBasisOf s_in_unf <| n + 1).val ‚äÜ UniformityBasisOf s_in_unf n := by
  rw [‚Üê Nat.succ_eq_add_one]
  exact (comp_symm_mem_uniformity_sets <| (UniformityBasisOf s_in_unf n).prop).choose_spec.2.2

theorem uniformity_le_uniformSpaceOf (s_in_unf : s ‚àà ùì§ X) :
    ùì§ X ‚â§ Filter.generate { (UniformSpace.UniformityBasisOf s_in_unf n).val | n : ‚Ñï } := by
  rw [Filter.le_generate_iff]
  intro r
  simp only [Set.mem_setOf_eq, Filter.mem_sets, forall_exists_index]
  intro n r_eq
  exact r_eq ‚ñ∏ (UniformityBasisOf s_in_unf n).prop

/--
The uniform space associated with a set `s ‚àà ùì§ X`:

* It is finitely generated (`UniformSpace.uniformSpaceOf_finitelyGenerated`).
* It is coarser than `ùì§ X` (`UniformSpace.uniformity_le_uniformSpaceOf`).
* The infimum of all the `UniformSpaceOf` is equal to the ambient uniform space
  (`UniformSpace.uniformity_eq_iInf_uniformSpaceOf`).
-/
def UniformSpaceOf (s_in_unf : s ‚àà ùì§ X) : UniformSpace X :=
  let uniformity := Filter.generate { (UniformSpace.UniformityBasisOf s_in_unf n).val | n : ‚Ñï }
  UniformSpace.ofCore {
    uniformity := uniformity
    refl := by
      intro r r_in_unf
      rw [Filter.mem_principal, idRel_subset]
      exact fun x => refl_mem_uniformity <| uniformity_le_uniformSpaceOf s_in_unf r_in_unf
    symm := by
      rw [Filter.Tendsto, Filter.le_generate_iff]
      intro r ‚ü®n, r_eq‚ü©
      rw [‚Üê r_eq, Filter.mem_sets, Filter.mem_map, UniformSpace.uniformityBasisOf_symmetric]
      apply Filter.mem_generate_of_mem
      use n
    comp := by
      rw [Filter.le_generate_iff]
      intro r ‚ü®n, r_eq‚ü©
      rw [‚Üê r_eq]
      apply Filter.mem_of_superset _ <| UniformSpace.uniformityBasisOf_comp_subset s_in_unf n
      apply (Filter.mem_lift'_sets <| Monotone.compRel monotone_id monotone_id).mpr
      exact ‚ü®_, Filter.mem_generate_of_mem ‚ü®n + 1, rfl‚ü©, subset_refl _‚ü©
  }

theorem self_mem_uniformSpaceOf (s_in_unf : s ‚àà ùì§ X) :
    s ‚àà (UniformSpaceOf s_in_unf).uniformity :=
  Filter.mem_of_superset (Filter.mem_generate_of_mem (Exists.intro 0 rfl))
    <| (symmetrizeRel_subset_self _)

/--
The ambient uniformity is equal to the infimum of all `UniformSpaceOf` for `s ‚àà ùì§ X`.
-/
theorem uniformity_eq_iInf_uniformSpaceOf :
    ùì§ X = ‚®Ö s, ‚®Ö (h : s ‚àà ùì§ X), ùì§[UniformSpaceOf h] :=
  _root_.le_antisymm
    (by
      simp_rw [le_iInf_iff]
      exact fun _ mem_unf => uniformity_le_uniformSpaceOf mem_unf)
    (fun (r : Set (X √ó X)) (r_in_unf : r ‚àà ùì§ X) =>
      Filter.mem_iInf_of_mem r
        <| Filter.mem_iInf_of_mem r_in_unf
        <| self_mem_uniformSpaceOf r_in_unf)

/--
The uniformity associated with `s ‚àà ùì§ X` is countably generated, so a pseudometric space
can be constructed for it.

Note that the uniformity associated with `s ‚àà ùì§ X` is in general
not equal to the ambient uniformity, see `UniformSpace.UniformSpaceOf` for more details.
-/
noncomputable def PseudoMetricSpaceOf (s_in_unf : s ‚àà ùì§ X) : PseudoMetricSpace X :=
  @UniformSpace.pseudoMetricSpace X (UniformSpaceOf s_in_unf)
    ‚ü®‚ü®_, Set.countable_range fun n => (UniformityBasisOf s_in_unf n).val, rfl‚ü©‚ü©

/--
If a set `t` is open within the topology generated by `UniformSpaceOf s_in_unf`,
then one can find some `Œµ > 0` for all of its points
-/
theorem isOpen_uniformSpaceOf_iff (s_in_unf : s ‚àà ùì§ X) {t : Set X} :
    IsOpen[(UniformSpaceOf s_in_unf).toTopologicalSpace] t ‚Üî
      ‚àÄ x ‚àà t, ‚àÉ Œµ, 0 < Œµ ‚àß @Metric.ball X (PseudoMetricSpaceOf s_in_unf) x Œµ ‚äÜ t :=
  @Metric.isOpen_iff X (PseudoMetricSpaceOf s_in_unf) _

/--
If `x ‚àà s` for some open `s`, then there exists a family of pseudometric spaces `D`
and an `Œµ > 0`, such that:
* `‚àÄ m ‚àà D`, the topology associated with `D` is coarser than the ambient topology on `X`.
* The intersection of the balls $$B_d(x, Œµ)$$ for all `d ‚àà D` is a subset of `s`.
-/
theorem exists_pseudoMetricSpace_family_of_isOpen {s : Set X} (s_open : IsOpen s)
    {x : X} (x_in_s : x ‚àà s) : ‚àÉ D : Set (PseudoMetricSpace X), D.Finite ‚àß
      (‚àÄ m ‚àà D, unf.toTopologicalSpace ‚â§ m.toUniformSpace.toTopologicalSpace) ‚àß
      ‚àÉ (Œµ : ‚Ñù), 0 < Œµ ‚àß ‚ãÇ m ‚àà D, @Metric.ball X m x Œµ ‚äÜ s := by
  rw [isOpen_iff_ball_subset] at s_open
  let ‚ü®V, V_in_unf, ball_subset‚ü© := s_open x x_in_s
  rw [uniformity_eq_iInf_uniformSpaceOf] at V_in_unf

  -- We firts obtain a family `t` of sets `r ‚àà ùì§ X`,
  -- as well as a map `f` such that `‚àÄ r ‚àà t, f r ‚àà ùì§[UniformSpaceOf r]`.
  rw [‚Üê iInf_subtype (f := fun pair : { t : Set (X √ó X) // t ‚àà ùì§ X } =>
    @uniformity X <| UniformSpaceOf pair.prop), Filter.mem_iInf] at V_in_unf
  have ‚ü®t, t_finite, f, f_mem_unf, V_eq‚ü© := V_in_unf
  rw [Set.Finite] at t_finite
  rw [V_eq] at ball_subset
  clear V_in_unf s_open V_eq V

  by_cases t_nonempty : Nonempty t; swap
  {
    -- If `t` is empty, then `s = Set.univ`, we don't need to provide any pseudometric space.
    rw [not_nonempty_iff] at t_nonempty
    simp only [ball, Set.iInter_of_empty, Set.preimage_univ, Set.univ_subset_iff] at ball_subset
    rw [ball_subset]
    use ‚àÖ
    simp only [Set.finite_empty, gt_iff_lt, Set.mem_empty_iff_false, Set.iInter_of_empty,
      Set.iInter_univ, Set.subset_univ, and_true, true_and]
    refine ‚ü®fun _ ff => ff.elim, ?eps‚ü©
    use 1
    exact zero_lt_one
  }

  -- We then construct a version of `interior ‚àò f`, where each set `f ‚ü®i, i_in_unf‚ü©` is mapped to
  -- `interior[UniformSpaceOf i_in_unf] (f i)`.
  let f' : t ‚Üí Set (X √ó X) := fun v =>
    let inst : TopologicalSpace X := (UniformSpaceOf v.val.prop).toTopologicalSpace
    @interior (X √ó X) (@instTopologicalSpaceProd _ _ inst inst) (f v)

  have x_in_ball_iInter : x ‚àà ball x (‚ãÇ i : t, f' i) := by
    apply UniformSpace.mem_ball_self
    rw [Filter.iInter_mem]
    intro i
    apply uniformity_le_uniformSpaceOf i.val.prop
    let _inst := UniformSpaceOf i.val.prop
    exact interior_mem_uniformity (f_mem_unf i)

  have ball_isOpen : ‚àÄ i : t, IsOpen[(UniformSpaceOf i.val.prop).toTopologicalSpace]
      (ball x (f' i)) := fun i =>
    let _inst := UniformSpaceOf i.val.prop
    isOpen_ball _ isOpen_interior

  -- Each `ball x (f' i)` is open in `UniformSpaceOf i_in_unf` and contains `x`,
  -- so we can use the pseudometric associated with `i` to get a set of `Œµ`,
  -- such that the metric ball associated with each `i` is a subset of the uniform ball
  -- associated with `i`.
  simp_rw [UniformSpace.isOpen_uniformSpaceOf_iff] at ball_isOpen
  replace ball_isOpen := fun i => ball_isOpen i x
    <| ball_mono (Set.iInter_subset _ i) x x_in_ball_iInter
  choose Œµ Œµ_pos ball_subset' using ball_isOpen

  -- We finally just need to show that the family of pseudometric space associated
  -- with elements of `t` and `iInf Œµ` satisfy our requirements.
  let D := (fun v => PseudoMetricSpaceOf v.prop) '' t
  refine ‚ü®D, Set.Finite.image _ t_finite, ?topology_le, ‚ü®iInf Œµ, ?pos, ?subset‚ü©‚ü©
  case pos =>
    rw [‚Üê sInf_range, Set.Finite.lt_cInf_iff (Set.finite_range Œµ) (Set.range_nonempty Œµ)]
    exact fun _ ‚ü®x, eq‚ü© => eq ‚ñ∏ Œµ_pos x
  case topology_le =>
    intro _ ‚ü®x, _, eq‚ü©
    rw [‚Üê eq]
    apply toTopologicalSpace_mono <| uniformity_le_uniformSpaceOf x.prop
  case subset =>
    show ‚ãÇ m ‚àà D, @Metric.ball _ m x (iInf Œµ) ‚äÜ s
    calc
      _ = ‚ãÇ (i : t), @Metric.ball X (PseudoMetricSpaceOf i.val.prop) x (iInf Œµ) := by
        simp_rw [Set.biInter_image, Set.iInter_subtype]
      _ ‚äÜ ‚ãÇ (i : t), @Metric.ball _ (PseudoMetricSpaceOf i.val.prop) x (Œµ i) := by
        apply Set.iInter_mono
        intro i
        let _inst := PseudoMetricSpaceOf i.val.prop
        apply Metric.ball_subset
        rw [dist_self, sub_nonneg]
        exact ciInf_le (Finite.bddBelow_range _) _
      _ ‚äÜ ‚ãÇ (i : t), ball x (f' i) := Set.iInter_mono ball_subset'
      _ ‚äÜ ‚ãÇ (i : t), ball x (f i) := by
        refine Set.iInter_mono fun i => ball_mono ?subset x
        let _inst := (UniformSpaceOf i.val.prop).toTopologicalSpace
        exact interior_subset
      _ = ball x (‚ãÇ (i : t), f i) := ball_iInter.symm
      _ ‚äÜ s := ball_subset

end PseudoMetrics

instance completelyRegularSpace : CompletelyRegularSpace X := by
  rw [completelyRegularSpace_iff]
  intro x K hK hx
  have hK' := hK.isOpen_compl
  rw [‚Üê Set.mem_compl_iff] at hx
  have ‚ü®t, t_finite, coarser, Œµ, Œµ_pos, subset‚ü© :=
    UniformSpace.exists_pseudoMetricSpace_family_of_isOpen hK' hx
  let ‚ü®finset, eq‚ü© := t_finite.exists_finset_coe

  by_cases finset_ne : finset.Nonempty; swap
  {
    -- If `t` is empty, then `K = X`, so a constant function suffices.
    rw [Finset.not_nonempty_iff_eq_empty] at finset_ne
    rw [finset_ne, Finset.coe_empty] at eq
    simp only [‚Üê eq, Set.mem_empty_iff_false, Set.iInter_of_empty, Set.iInter_univ,
      Set.univ_subset_iff, Set.compl_univ_iff] at subset
    rw [subset]
    exact ‚ü®fun _ => 0, continuous_const, rfl, Set.eqOn_empty _ _‚ü©
  }

  -- f is a continuous function that maps `x` to `0` and points inside of `K` to values `‚â• Œµ`
  let f := fun y : X => finset.sup' finset_ne fun m => m.dist x y
  have f_cont : Continuous f := by
    apply Continuous.finset_sup'_apply finset_ne
    intro m mem
    apply Continuous.dist <| @continuous_const _ _ _ m.toUniformSpace.toTopologicalSpace _

    show Continuous[unf.toTopologicalSpace, m.toUniformSpace.toTopologicalSpace] id
    rw [continuous_id_iff_le]
    simp only [‚Üê eq, Finset.mem_coe] at coarser
    exact coarser m mem

  have f_ge : ‚àÄ y ‚àà K, Œµ ‚â§ f y := by
    intro y yK
    simp_rw [Set.subset_compl_comm, Set.compl_iInter] at subset
    apply subset at yK
    simp_rw [Set.mem_iUnion, Set.mem_compl_iff, ‚Üê eq, Finset.mem_coe, Metric.mem_ball] at yK
    let ‚ü®m, m_in_t, y_notin_ball‚ü© := yK
    rw [‚Üê swap_dist] at y_notin_ball
    exact (Finset.le_sup'_iff _).mpr ‚ü®m, m_in_t, by simpa using y_notin_ball‚ü©

  have f_pos : ‚àÄ y, 0 ‚â§ f y := fun y =>
    let ‚ü®m, m_in_finset‚ü© := finset_ne
    (Finset.le_sup'_iff _).mpr ‚ü®m, m_in_finset, dist_nonneg‚ü©

  -- We obtain our target function by clamping `f` to `Œµ` and dividing it by `Œµ`.
  refine ‚ü®fun y : X => ‚ü®(f y ‚äì Œµ) / Œµ, unitInterval.div_mem
      (le_inf_iff.mpr ‚ü®f_pos y, Œµ_pos.le‚ü©)
      Œµ_pos.le
      inf_le_right‚ü©,
    Continuous.subtype_mk (Continuous.div_const (Continuous.inf f_cont continuous_const) _) _,
    ?eq_zero,
    ?eqOn_one‚ü©

  case eq_zero =>
    rw [Subtype.mk.injEq]
    simp only [dist_self, Finset.sup'_const, Set.Icc.coe_zero, div_eq_zero_iff, inf_eq_left]
    left
    exact Œµ_pos.le
  case eqOn_one =>
    intro y yK
    rw [Subtype.mk.injEq, Pi.one_apply, Set.Icc.coe_one, div_eq_one_iff_eq Œµ_pos.ne']
    exact inf_eq_right.mpr (f_ge y yK)
