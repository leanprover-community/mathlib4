/-
Copyright (c) 2020 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Yury Kudryashov
-/
import Mathlib.Topology.Separation.Regular
import Mathlib.Topology.UniformSpace.Defs
import Mathlib.Tactic.TautoSet

/-!
# Compact separated uniform spaces

## Main statement

* `uniformSpaceOfCompactR1`: every compact R1 topological structure is induced by a uniform
  structure. This uniform structure is described by `compactSpace_uniformity`.

## Implementation notes

The construction `uniformSpaceOfCompactR1` is not declared as an instance, as it would badly
loop.

## Tags

uniform space, uniform continuity, compact space
-/

open Topology Filter UniformSpace Set
open scoped SetRel

variable {Î³ : Type*}

/-!
### Uniformity on compact spaces
-/


/-- The unique uniform structure inducing a given compact topological structure. -/
def uniformSpaceOfCompactR1 [TopologicalSpace Î³] [CompactSpace Î³] [R1Space Î³] : UniformSpace Î³ where
  uniformity := ğ“Ë¢ (diagonal Î³)
  symm := continuous_swap.tendsto_nhdsSet fun _ => Eq.symm
  comp := by
      /-
        This is the difficult part of the proof. We need to prove that, for each neighborhood `W`
        of the diagonal `Î”`, there exists a smaller neighborhood `V` such that `V â—‹ V âŠ† W`.
      -/
    set ğ“Î” := ğ“Ë¢ (diagonal Î³)
    -- The filter of neighborhoods of Î”
    set F := ğ“Î”.lift' fun s : Set (Î³ Ã— Î³) => s â—‹ s
    -- Compositions of neighborhoods of Î”
    -- If this weren't true, then there would be V âˆˆ ğ“Î” such that F âŠ“ ğ“Ÿ Vá¶œ â‰  âŠ¥
    rw [le_iff_forall_inf_principal_compl]
    intro V V_in
    by_contra H
    haveI : NeBot (F âŠ“ ğ“Ÿ Vá¶œ) := âŸ¨HâŸ©
    -- Hence compactness would give us a cluster point (x, y) for F âŠ“ ğ“Ÿ Vá¶œ
    obtain âŸ¨âŸ¨x, yâŸ©, hxyâŸ© : âˆƒ p : Î³ Ã— Î³, ClusterPt p (F âŠ“ ğ“Ÿ Vá¶œ) := exists_clusterPt_of_compactSpace _
    -- In particular (x, y) is a cluster point of ğ“Ÿ Vá¶œ, hence is not in the interior of V,
    -- so x and y are inseparable
    have clV : ClusterPt (x, y) (ğ“Ÿ <| Vá¶œ) := hxy.of_inf_right
    have ninsep_xy : Â¬Inseparable x y := by
      intro h
      rw [â† mem_closure_iff_clusterPt, (h.prod .rfl).mem_closed_iff isClosed_closure,
        closure_compl] at clV
      apply clV
      rw [mem_interior_iff_mem_nhds]
      exact nhds_le_nhdsSet (mem_diagonal y) V_in
    -- Since Î³ is compact and Hausdorff, it is Tâ‚„, hence Tâ‚ƒ.
    -- So there are closed neighborhoods Vâ‚ and Vâ‚‚ of x and y contained in
    -- disjoint open neighborhoods Uâ‚ and Uâ‚‚.
    obtain
      âŸ¨Uâ‚, -, Vâ‚, Vâ‚_in, Uâ‚‚, -, Vâ‚‚, Vâ‚‚_in, Vâ‚_cl, Vâ‚‚_cl, Uâ‚_op, Uâ‚‚_op, VUâ‚, VUâ‚‚, hUâ‚â‚‚âŸ© :=
      disjoint_nested_nhds_of_not_inseparable ninsep_xy
    -- We set Uâ‚ƒ := (Vâ‚ âˆª Vâ‚‚)á¶œ so that W := Uâ‚ Ã—Ë¢ Uâ‚ âˆª Uâ‚‚ Ã—Ë¢ Uâ‚‚ âˆª Uâ‚ƒ Ã—Ë¢ Uâ‚ƒ is an open
    -- neighborhood of Î”.
    let Uâ‚ƒ := (Vâ‚ âˆª Vâ‚‚)á¶œ
    have Uâ‚ƒ_op : IsOpen Uâ‚ƒ := (Vâ‚_cl.union Vâ‚‚_cl).isOpen_compl
    let W := Uâ‚ Ã—Ë¢ Uâ‚ âˆª Uâ‚‚ Ã—Ë¢ Uâ‚‚ âˆª Uâ‚ƒ Ã—Ë¢ Uâ‚ƒ
    have W_in : W âˆˆ ğ“Î” := by
      rw [mem_nhdsSet_iff_forall]
      rintro âŸ¨z, z'âŸ© hzz'
      refine IsOpen.mem_nhds ?_ ?_
      Â· apply_rules [IsOpen.union, IsOpen.prod]
      Â· cases hzz'
        simp only [W, Uâ‚ƒ, mem_union, mem_prod]
        tauto_set
    -- So W â—‹ W âˆˆ F by definition of F
    have : W â—‹ W âˆˆ F := mem_lift' W_in
    -- And Vâ‚ Ã—Ë¢ Vâ‚‚ âˆˆ ğ“ (x, y)
    have hVâ‚â‚‚ : Vâ‚ Ã—Ë¢ Vâ‚‚ âˆˆ ğ“ (x, y) := prod_mem_nhds Vâ‚_in Vâ‚‚_in
    -- But (x, y) is also a cluster point of F so (Vâ‚ Ã—Ë¢ Vâ‚‚) âˆ© (W â—‹ W) â‰  âˆ…
    obtain âŸ¨âŸ¨u, vâŸ©, âŸ¨u_in, v_inâŸ©, w, huw, hwvâŸ© :=
      clusterPt_iff_nonempty.mp hxy.of_inf_left hVâ‚â‚‚ this
    -- However the construction of W implies (Vâ‚ Ã—Ë¢ Vâ‚‚) âˆ© (W â—‹ W) = âˆ….
    simp only [W, Uâ‚ƒ, mem_union, mem_prod] at huw hwv
    tauto_set
  nhds_eq_comap_uniformity x := by
    simp_rw [nhdsSet_diagonal, comap_iSup, nhds_prod_eq, comap_prod, Function.comp_def, comap_id']
    rw [iSup_split _ (Inseparable x)]
    conv_rhs =>
      congr
      Â· enter [1, i, 1, hi]
        rw [â† hi.nhds_eq, comap_const_of_mem fun V => mem_of_mem_nhds]
      Â· enter [1, i, 1, hi]
        rw [(not_specializes_iff_exists_open.1 (mt Specializes.inseparable hi)).elim
          fun S hS => comap_const_of_notMem (hS.1.mem_nhds hS.2.1) hS.2.2]
    rw [iSup_subtype', @iSup_const _ _ _ _ âŸ¨âŸ¨x, .rflâŸ©âŸ©]
    simp
