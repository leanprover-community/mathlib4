/-
Copyright (c) 2025 Aaron Liu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Aaron Liu
-/
module

public import Mathlib.Topology.Separation.CompletelyRegular

import Mathlib.Topology.UniformSpace.OfCompactT2
import Mathlib.Topology.UrysohnsLemma

/-!
# Uniformizable Spaces

A topological space is uniformizable (there exists a uniformity that
generates the same topology) iff it is completely regular.

TODO: Explain proofs

## Main Results

* `UniformSpace.toCompletelyRegularSpace`: Uniform spaces are completely regular
* `CompletelyRegularSpace.exists_uniformSpace`: Completely regular spaces are uniformizable
* `CompletelyRegularSpace.of_exists_uniformSpace`: Uniformizable spaces are completely regular
* `completelyRegularSpace_iff_exists_uniformSpace`: A space is completely regular
  iff it is uniformizable

## Implementation Details

Urysohn's lemma is reused in the proof of `UniformSpace.completelyRegularSpace`.

## References

* <https://www.math.wm.edu/~vinroot/PadicGroups/519probset1.pdf>
-/

variable {Œ± : Type*}

open Filter Set Uniformity SetRel

section UniformSpace
variable [UniformSpace Œ±]

def P (c : Set Œ±) (u : Set Œ±) :=
  ‚àÉ (x : Œ±) (uc uu s : SetRel Œ± Œ±),
    IsOpen uc ‚àß uc.IsSymm ‚àß uc ‚àà ùì§ Œ± ‚àß c = closure (Prod.mk x ‚Åª¬π' uc) ‚àß
    IsOpen uu ‚àß u = Prod.mk x ‚Åª¬π' uu ‚àß s ‚óã uc ‚óã s ‚äÜ uu ‚àß s ‚àà ùì§ Œ±

theorem urysohns_main {c u : Set Œ±} (Pcu : P c u) :
    ‚àÉ (v : Set Œ±), IsOpen v ‚àß c ‚äÜ v ‚àß closure v ‚äÜ u ‚àß P c v ‚àß P (closure v) u := by
  obtain ‚ü®x, uc, uu, s, huc, symmuc, ucu, rfl, huu, rfl, hn, hs‚ü© := Pcu
  obtain ‚ü®(ds : SetRel Œ± Œ±), hdsu, hdso, hdss, hdsd‚ü© := comp_open_symm_mem_uniformity_sets hs
  have ho : IsOpen (ds ‚óã uc ‚óã ds) := (hdso.relComp huc).relComp hdso
  use Prod.mk x ‚Åª¬π' (ds ‚óã uc ‚óã ds), ho.preimage (Continuous.prodMk_right x)
  constructor
  ¬∑ apply ((Continuous.prodMk_right x).closure_preimage_subset _).trans
    apply Set.preimage_mono
    rw [closure_eq_inter_uniformity, comp_assoc]
    exact iInter‚ÇÇ_subset ds hdsu
  constructor
  ¬∑ apply ((Continuous.prodMk_right x).closure_preimage_subset _).trans
    apply Set.preimage_mono
    apply hn.trans'
    rw [closure_eq_inter_uniformity]
    apply iInter‚ÇÇ_subset_of_subset ds hdsu
    exact Eq.trans_subset (by simp_rw [comp_assoc])
      (comp_subset_comp (comp_subset_comp hdsd subset_rfl) hdsd)
  have : ds.IsRefl := id_subset_iff.1 (refl_le_uniformity hdsu)
  have hucd : ds ‚óã uc ‚óã ds ‚àà ùì§ Œ± :=
    mem_of_superset ucu (right_subset_comp.trans left_subset_comp)
  constructor
  ¬∑ exact ‚ü®x, uc, ds ‚óã uc ‚óã ds, ds, huc, symmuc, ucu, rfl, ho, rfl, subset_rfl, hdsu‚ü©
  ¬∑ have hos : (ds ‚óã uc ‚óã ds).IsSymm := by
      rw [‚Üê inv_eq_self_iff, inv_comp, inv_comp, inv_eq_self, inv_eq_self, comp_assoc]
    refine ‚ü®x, ds ‚óã uc ‚óã ds, uu, ds, ho, hos, hucd, rfl, huu, rfl, ?_, hdsu‚ü©
    calc ds ‚óã (ds ‚óã uc ‚óã ds) ‚óã ds
      _ = (ds ‚óã ds) ‚óã uc ‚óã (ds ‚óã ds) := by simp [comp_assoc]
      _ ‚äÜ s ‚óã uc ‚óã s := comp_subset_comp (comp_subset_comp hdsd subset_rfl) hdsd
      _ ‚äÜ uu := hn

public instance UniformSpace.toCompletelyRegularSpace : CompletelyRegularSpace Œ± where
  completely_regular x K hK hx := by
    obtain ‚ü®O, hOu, hOo, hbO‚ü© := isOpen_iff_isOpen_ball_subset.mp hK.isOpen_compl x hx
    obtain ‚ü®C, hCu, hCc, hCO‚ü© := mem_uniformity_isClosed hOu
    obtain ‚ü®(u3 : SetRel Œ± Œ±), hu3u, hu3C‚ü© := comp3_mem_uniformity hCu
    obtain ‚ü®(ds : SetRel Œ± Œ±), hdsu, hdso, hdss, hdsd‚ü© := comp_open_symm_mem_uniformity_sets hu3u
    have : u3.IsRefl := id_subset_iff.1 (refl_le_uniformity hu3u)
    have : ds.IsRefl := id_subset_iff.1 (refl_le_uniformity hdsu)
    replace hdsd : ds ‚äÜ u3 := left_subset_comp.trans hdsd
    have hxc : x ‚àà closure (Prod.mk x ‚Åª¬π' ds) := subset_closure (refl_mem_uniformity hdsu)
    have hyo : K ‚äÜ (Prod.mk x ‚Åª¬π' O)·∂ú := subset_compl_comm.mpr hbO
    set c : Urysohns.CU P := {
      C := closure (Prod.mk x ‚Åª¬π' ds)
      U := Prod.mk x ‚Åª¬π' O
      closed_C := isClosed_closure
      open_U := hOo.preimage (Continuous.prodMk_right x)
      subset := (closure_minimal (Set.preimage_mono
        ((hdsd.trans left_subset_comp).trans hu3C))
          (hCc.preimage (.prodMk_right x))).trans (preimage_mono hCO)
      hP _ Pcu _ _ := urysohns_main Pcu
      P_C_U := ‚ü®x, ds, O, u3, hdso, hdss, hdsu, rfl, hOo, rfl,
        ((comp_assoc u3 ds u3).trans_le (comp_subset_comp_right
          (comp_subset_comp_left hdsd))).trans (hu3C.trans hCO), hu3u‚ü©
    }
    exact ‚ü®fun x => ‚ü®c.lim x, c.lim_mem_Icc x‚ü©, c.continuous_lim.subtype_mk c.lim_mem_Icc,
      Subtype.ext (c.lim_of_mem_C x hxc), fun y hy => Subtype.ext (c.lim_of_notMem_U y (hyo hy))‚ü©

end UniformSpace

section TopologicalSpace
variable [t : TopologicalSpace Œ±]

public theorem CompletelyRegularSpace.of_exists_uniformSpace
    (h : ‚àÉ (u : UniformSpace Œ±), u.toTopologicalSpace = t) :
    CompletelyRegularSpace Œ± := by
  obtain ‚ü®u, rfl‚ü© := h
  infer_instance

section CompletelyRegularSpace
variable [CompletelyRegularSpace Œ±]

public theorem CompletelyRegularSpace.exists_uniformSpace :
    ‚àÉ (u : UniformSpace Œ±), u.toTopologicalSpace = t :=
  ‚ü®uniformSpaceOfCompactR1.comap stoneCechUnit, isInducing_stoneCechUnit.eq_induced.symm‚ü©

end CompletelyRegularSpace

public theorem completelyRegularSpace_iff_exists_uniformSpace :
    CompletelyRegularSpace Œ± ‚Üî ‚àÉ (u : UniformSpace Œ±), u.toTopologicalSpace = t :=
  ‚ü®@CompletelyRegularSpace.exists_uniformSpace Œ± t, CompletelyRegularSpace.of_exists_uniformSpace‚ü©

end TopologicalSpace
