/-
Copyright (c) 2025 Aaron Liu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Aaron Liu
-/
import Mathlib.Topology.Separation.CompletelyRegular

/-!
# Uniformizable Spaces

A topological space is uniformizable (there exists a uniformity that
generates the same topology) iff it is completely regular.

TODO: Explain proofs

## Main Results

* `UniformSpace.completelyRegularSpace`: Uniform spaces are completely regular
* `CompletelyRegularSpace.exists_uniformSpace`: Completely regular spaces are uniformizable
* `CompletelyRegularSpace.of_exists_uniformSpace`: Uniformizable spaces are completely regular
* `completelyRegularSpace_iff_exists_uniformSpace`: A space is completely regular
  iff it is uniformizable

## Implementation Details

Urysohn's lemma is reused in the proof of `UniformSpace.completelyRegularSpace`.

## References

* <https://www.math.wm.edu/~vinroot/PadicGroups/519probset1.pdf>
* [S. Willard, *General Topology*][zbMATH02107988]
-/

variable {Œ± : Type*}

open Filter Set Uniformity

section UniformSpace
variable [UniformSpace Œ±]

private noncomputable def descend (s : { s : Set (Œ± √ó Œ±) // s ‚àà ùì§ Œ± }) :
    { s : Set (Œ± √ó Œ±) // s ‚àà ùì§ Œ± } :=
  ‚ü®_, (comp_open_symm_mem_uniformity_sets (mem_uniformity_isClosed
    (comp_open_symm_mem_uniformity_sets s.2).choose_spec.1).choose_spec.1).choose_spec.1‚ü©

private theorem descend_open (s : { s : Set (Œ± √ó Œ±) // s ‚àà ùì§ Œ± }):
    IsOpen (descend s).1 :=
  (comp_open_symm_mem_uniformity_sets (mem_uniformity_isClosed
    (comp_open_symm_mem_uniformity_sets s.2).choose_spec.1).choose_spec.1).choose_spec.2.1

private theorem descend_symm (s : { s : Set (Œ± √ó Œ±) // s ‚àà ùì§ Œ± }) :
    IsSymmetricRel (descend s).1 :=
  (comp_open_symm_mem_uniformity_sets (mem_uniformity_isClosed
    (comp_open_symm_mem_uniformity_sets s.2).choose_spec.1).choose_spec.1).choose_spec.2.2.1

private theorem descend_descends (s : { s : Set (Œ± √ó Œ±) // s ‚àà ùì§ Œ± }) :
    (descend s).1 ‚óã (descend s).1 ‚äÜ s := by
  dsimp [descend]
  generalize_proofs o‚ÇÅ c o‚ÇÇ
  have hoc : o‚ÇÇ.choose ‚äÜ c.choose := by
    trans o‚ÇÇ.choose ‚óã o‚ÇÇ.choose
    ¬∑ apply subset_comp_self
      rw [‚Üê Filter.mem_principal]
      exact refl_le_uniformity o‚ÇÇ.choose_spec.1
    ¬∑ exact o‚ÇÇ.choose_spec.2.2.2
  calc o‚ÇÇ.choose ‚óã o‚ÇÇ.choose
    _ ‚äÜ c.choose ‚óã c.choose := compRel_mono hoc hoc
    _ ‚äÜ o‚ÇÅ.choose ‚óã o‚ÇÅ.choose := compRel_mono c.choose_spec.2.2 c.choose_spec.2.2
    _ ‚äÜ s.1 := o‚ÇÅ.choose_spec.2.2.2

private def P (c : Set Œ±) (u : Set Œ±) :=
  ‚àÉ (x : Œ±) (uc uu : Set (Œ± √ó Œ±)) (s : { s : Set (Œ± √ó Œ±) // s ‚àà ùì§ Œ± }),
    IsOpen uc ‚àß IsSymmetricRel uc ‚àß uc ‚àà ùì§ Œ± ‚àß c = closure (Prod.mk x ‚Åª¬π' uc) ‚àß
    IsOpen uu ‚àß u = Prod.mk x ‚Åª¬π' uu ‚àß s ‚óã uc ‚óã s ‚äÜ uu

private theorem descend_spec {c u : Set Œ±} (Pcu : P c u) :
    ‚àÉ (v : Set Œ±), IsOpen v ‚àß c ‚äÜ v ‚àß closure v ‚äÜ u ‚àß P c v ‚àß P (closure v) u := by
  obtain ‚ü®x, uc, uu, s, huc, symmuc, ucu, rfl, huu, rfl, hn‚ü© := Pcu
  have ho : IsOpen (descend s ‚óã uc ‚óã descend s) :=
    ((descend_open s).compRel huc).compRel (descend_open s)
  use Prod.mk x ‚Åª¬π' (descend s ‚óã uc‚óã descend s), ho.preimage (Continuous.prodMk_right x)
  constructor
  ¬∑ apply ((Continuous.prodMk_right x).closure_preimage_subset _).trans
    apply preimage_mono
    rw [closure_eq_inter_uniformity, compRel_assoc]
    exact iInter‚ÇÇ_subset (descend s).1 (descend s).2
  constructor
  ¬∑ apply ((Continuous.prodMk_right x).closure_preimage_subset _).trans
    apply preimage_mono
    apply hn.trans'
    rw [closure_eq_inter_uniformity]
    apply iInter‚ÇÇ_subset_of_subset (descend s).1 (descend s).2
    exact Eq.trans_subset (by simp [compRel_assoc])
      (compRel_mono (compRel_mono (descend_descends s) subset_rfl) (descend_descends s))
  have hucd : descend s ‚óã uc ‚óã descend s ‚àà ùì§ Œ± :=
    mem_of_superset ucu
      ((right_subset_compRel (refl_le_uniformity (descend s).2)).trans
        (left_subset_compRel (refl_le_uniformity (descend s).2)))
  constructor
  ¬∑ exact ‚ü®x, uc, _, _, huc, symmuc, ucu, rfl, ho, rfl, subset_rfl‚ü©
  ¬∑ have hos : IsSymmetricRel (descend s ‚óã uc ‚óã descend s) := by
      simp [IsSymmetricRel, compRel_assoc, prodSwap_preimage_compRel,
        symmuc.eq, (descend_symm s).eq]
    refine ‚ü®x, _, uu, descend s, ho, hos, hucd, rfl, huu, rfl, ?_‚ü©
    calc descend s ‚óã (descend s ‚óã uc ‚óã descend s) ‚óã descend s
      _ = (descend s ‚óã descend s) ‚óã uc ‚óã (descend s ‚óã descend s) := by simp [compRel_assoc]
      _ ‚äÜ s ‚óã uc ‚óã s :=
        compRel_mono (compRel_mono (descend_descends s) subset_rfl) (descend_descends s)
      _ ‚äÜ uu := hn

instance UniformSpace.toCompletelyRegularSpace : CompletelyRegularSpace Œ± where
  completely_regular x K hK hx := by
    obtain ‚ü®O, hOu, hOo, hbO‚ü© := isOpen_iff_isOpen_ball_subset.mp hK.isOpen_compl x hx
    have hcu := (descend (descend ‚ü®O, hOu‚ü©)).2
    have hccccO :=
      (compRel_mono
        (descend_descends (descend ‚ü®O, hOu‚ü©))
        (descend_descends (descend ‚ü®O, hOu‚ü©))).trans
      (descend_descends ‚ü®O, hOu‚ü©)
    obtain ‚ü®C, hCu, hC, hCc‚ü© := mem_uniformity_isClosed hcu
    have hCO := calc
      _ ‚äÜ _ := hCc
      _ ‚äÜ _ := subset_comp_self_of_mem_uniformity hcu
      _ ‚äÜ _ := subset_comp_self_of_mem_uniformity
        (mem_of_superset hcu (subset_comp_self_of_mem_uniformity hcu))
      _ ‚äÜ _ := hccccO
    have hou := (descend ‚ü®C, hCu‚ü©).2
    have hoo := descend_open ‚ü®C, hCu‚ü©
    have hosymm := descend_symm ‚ü®C, hCu‚ü©
    have hooC := descend_descends ‚ü®C, hCu‚ü©
    have hoC := (subset_comp_self_of_mem_uniformity hou).trans hooC
    have hxo : x ‚àà closure (Prod.mk x ‚Åª¬π' (descend ‚ü®C, hCu‚ü©).1) :=
      subset_closure (mem_ball_self x hou)
    have hyo : K ‚äÜ (Prod.mk x ‚Åª¬π' O)·∂ú := subset_compl_comm.mp hbO
    set c : Urysohns.CU P := {
      C := closure (Prod.mk x ‚Åª¬π' (descend ‚ü®C, hCu‚ü©).1)
      U := Prod.mk x ‚Åª¬π' O
      closed_C := isClosed_closure
      open_U := hOo.preimage (Continuous.prodMk_right x)
      subset := (closure_minimal (preimage_mono hoC) (isClosed_ball x hC)).trans (preimage_mono hCO)
      hP _ Pcu _ _ := descend_spec Pcu
      P_C_U := by
        exact ‚ü®x, descend ‚ü®C, hCu‚ü©, O, _, hoo, hosymm, hou, rfl, hOo, rfl,
          (compRel_mono (compRel_mono subset_rfl (hoC.trans hCc))
            (subset_comp_self_of_mem_uniformity (descend (descend ‚ü®O, hOu‚ü©)).2)).trans hccccO‚ü©
    }
    exact ‚ü®fun x => ‚ü®c.lim x, c.lim_mem_Icc x‚ü©, c.continuous_lim.subtype_mk c.lim_mem_Icc,
      Subtype.ext (c.lim_of_mem_C x hxo), fun y hy => Subtype.ext (c.lim_of_notMem_U y (hyo hy))‚ü©

end UniformSpace

section TopologicalSpace
variable [t : TopologicalSpace Œ±]

variable (Œ±) in
/-
This definition could be useful in other places, but I've not seen a use yet
so this is private for now (mainly so I don't have to write api).
-/
private def inducedUniformity : UniformSpace Œ± :=
  UniformSpace.ofCore {
    uniformity := ‚®Ö (f : C(Œ±, ‚Ñù)) (Œµ : ‚Ñù) (_ : Œµ > 0),
      principal ((fun p => dist (f p.fst) (f p.snd)) ‚Åª¬π' Iio Œµ)
    refl := by
      simp_rw [le_iInf_iff]
      simp +contextual
    symm := by
      rw [tendsto_iff_comap]
      apply le_of_eq
      simp_rw [Filter.comap_iInf, comap_principal, preimage_preimage,
        Prod.fst_swap, Prod.snd_swap, dist_comm]
    -- this proof takes four seconds to elaborate on my machine
    -- TODO: speed this up
    comp := by
      intro s hs
      simp_rw [mem_iInf, iInf_eq_if] at hs
      obtain ‚ü®I, hI, U, hs, rfl‚ü© := hs
      have := hI.to_subtype
      choose J hJ V hs hU using hs
      have := fun i => (hJ i).to_subtype
      replace hU : U = fun i => ‚ãÇ j, V i j := funext hU
      subst hU
      suffices h : ((fun s => s ‚óã s) <| ‚ãÇ (i : I) (j : J i) (_ : j.val > 0),
          (fun p ‚Ü¶ dist (i.val p.fst) (i.val p.snd)) ‚Åª¬π' Iio (j / 2)) ‚äÜ ‚ãÇ (i) (j), V i j by
        refine mem_of_superset ?_ h
        apply mem_lift'
        simp_rw [mem_iInf, iInf_eq_if, mem_ite, mem_principal, mem_top]
        refine ‚ü®I, hI, _, fun i => ?_, rfl‚ü©
        refine ‚ü®(¬∑ / 2) '' J i, (hJ i).image (¬∑ / 2),
          fun j => ‚ãÇ (_ : j.val > 0), (fun p ‚Ü¶ dist (i.val p.fst) (i.val p.snd)) ‚Åª¬π' Iio j,
            fun j => ‚ü®fun hj => ?_, fun hj => by simp [hj]‚ü©, ?_‚ü©
        ¬∑ simp_rw [iInter_eq_if, if_pos hj, subset_rfl]
        ¬∑ apply iInter_congr_of_surjective _ surjective_onto_image
          intro j
          simp only [imageFactorization]
          exact iInter_congr_Prop (by simp) fun _ => rfl
      simp_rw [subset_iInter_iff]
      intro i j
      specialize hs i j
      split_ifs at hs with hj
      ¬∑ intro x hx
        apply hs
        rw [mem_compRel] at hx
        obtain ‚ü®z, hxz, hzx‚ü© := hx
        simp_rw [mem_iInter] at hxz hzx
        apply (dist_triangle (i.val x.fst) (i.val z) (i.val x.snd)).trans_lt
        rw [‚Üê add_halves j.val]
        exact add_lt_add (hxz i j hj) (hzx i j hj)
      ¬∑ rw [mem_top] at hs
        simp [hs]
  }

private theorem le_u_l : t ‚â§ (inducedUniformity Œ±).toTopologicalSpace := by
  intro s hs
  rw [isOpen_iff_forall_mem_open]
  rw [@isOpen_iff_ball_subset] at hs
  intro x hx
  obtain ‚ü®V, hV, hVs‚ü© := hs x hx
  change V ‚àà iInf _ at hV
  simp_rw [mem_iInf] at hV
  obtain ‚ü®I, hI, U, hU, rfl‚ü© := hV
  have := hI.to_subtype
  choose J hJ V hV hU using hU
  have := fun i => (hJ i).to_subtype
  replace hU : U = fun i => ‚ãÇ j, V i j := funext hU
  subst hU
  refine ‚ü®‚ãÇ (i : I) (j : J i) (_ : j.val > 0), (fun y => dist (i.val x) (i.val y)) ‚Åª¬π' Iio j,
    ?_, ?_, ?_‚ü©
  ¬∑ apply hVs.trans'
    simp_rw [UniformSpace.ball, preimage_iInter]
    apply iInter‚ÇÇ_mono
    intro i j
    simp_rw [iInf_eq_if, mem_ite, mem_principal, mem_top] at hV
    rw [iInter_eq_if]
    split_ifs with hj
    ¬∑ apply (preimage_mono ((hV i j).left hj)).trans'
      intro y hy
      simpa using hy
    ¬∑ simp [(hV i j).right hj]
  ¬∑ exact isOpen_iInter_of_finite fun i =>
      isOpen_iInter_of_finite fun j =>
        isOpen_iInter_of_finite fun hj =>
          isOpen_Iio.preimage (continuous_const.dist i.val.continuous)
  ¬∑ simp_rw [mem_iInter]
    intro i j hj
    simp [hj]

section CompletelyRegularSpace
variable [CompletelyRegularSpace Œ±]

private theorem u_l_le : (inducedUniformity Œ±).toTopologicalSpace ‚â§ t := by
  intro s hs
  rw [@isOpen_iff_ball_subset]
  intro x hx
  obtain ‚ü®f, hf, hf0, hf1‚ü© :=
    CompletelyRegularSpace.completely_regular x s·∂ú hs.isClosed_compl (notMem_compl_iff.mpr hx)
  use ((fun p : Œ± √ó Œ± => dist (f p.fst : ‚Ñù) (f p.snd)) ‚Åª¬π' Iio 2‚Åª¬π)
  constructor
  ¬∑ have hf' : Continuous (fun i => (f i : ‚Ñù)) := continuous_subtype_val.comp hf
    suffices h : (_ : Filter _) ‚â§ _ from h (mem_principal_self _)
    apply iInf‚ÇÇ_le_of_le ‚ü®_, hf'‚ü© 2‚Åª¬π
    exact iInf_le _ (by norm_num)
  ¬∑ rw [UniformSpace.ball]
    intro a ha
    by_contra has
    norm_num [hf0, hf1 has] at ha

theorem CompletelyRegularSpace.exists_uniformSpace :
    ‚àÉ (u : UniformSpace Œ±), u.toTopologicalSpace = t :=
  ‚ü®inducedUniformity Œ±, u_l_le.antisymm le_u_l‚ü©

end CompletelyRegularSpace

theorem CompletelyRegularSpace.of_exists_uniformSpace
    (h : ‚àÉ (u : UniformSpace Œ±), u.toTopologicalSpace = t) :
    CompletelyRegularSpace Œ± := by
  obtain ‚ü®u, rfl‚ü© := h
  infer_instance

theorem completelyRegularSpace_iff_exists_uniformSpace :
    CompletelyRegularSpace Œ± ‚Üî ‚àÉ (u : UniformSpace Œ±), u.toTopologicalSpace = t :=
  ‚ü®@CompletelyRegularSpace.exists_uniformSpace Œ± t, CompletelyRegularSpace.of_exists_uniformSpace‚ü©

end TopologicalSpace
