/-
Copyright (c) 2025 Aaron Liu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Aaron Liu
-/
module

public import Mathlib.Topology.Separation.CompletelyRegular

import Mathlib.Topology.UniformSpace.OfCompactT2
import Mathlib.Topology.UrysohnsLemma

/-!
# Uniformizable Spaces

A topological space is uniformizable (there exists a uniformity that
generates the same topology) iff it is completely regular.

## Main Results

* `UniformSpace.toCompletelyRegularSpace`: Uniform spaces are completely regular
* `CompletelyRegularSpace.exists_uniformSpace`: Completely regular spaces are uniformizable
* `CompletelyRegularSpace.of_exists_uniformSpace`: Uniformizable spaces are completely regular
* `completelyRegularSpace_iff_exists_uniformSpace`: A space is completely regular
  iff it is uniformizable

## Implementation Details

Urysohn's lemma is reused in the proof of `UniformSpace.completelyRegularSpace`.

## References

* <https://www.math.wm.edu/~vinroot/PadicGroups/519probset1.pdf>
-/

variable {Œ± : Type*}

open Filter Set Uniformity UniformSpace SetRel

section UniformSpace
variable [UniformSpace Œ±]

/-- To construct a real-valued function separating a point `x` from a closed set in a uniform
space, we recursively construct pairs of a closed set `c` contained in an open set `u`
satisfying the following predicate: the closed set is the closure of the ball centered
at `x` associated to some open entourage `uc`, the open set is the ball centered at `x`
associated to some entourage `uu`, such that `uu` is a thickening of `uc` by some entourage `s`
in the sense that the composition `s ‚óã uc ‚óã s` is contained in `uu`. -/
def P (c u : Set Œ±) :=
  ‚àÉ (x : Œ±) (uc uu s : SetRel Œ± Œ±),
    IsOpen uc ‚àß uc ‚àà ùì§ Œ± ‚àß c = closure (ball x uc) ‚àß
    u = ball x uu ‚àß s ‚óã uc ‚óã s ‚äÜ uu ‚àß s ‚àà ùì§ Œ±

/-- Given a pair consisting of a closed set `c` contained in an open set `u` satisfying the
predicate `P`, it is always possible to refine it to two pairs `c ‚äÜ v` and `closure v ‚äÜ u`
satisfying `P`. We can then use the general `Urysohns.CU` construction to obtain the
desired real-valued function. -/
theorem urysohns_main {c u : Set Œ±} (Pcu : P c u) :
    ‚àÉ (v : Set Œ±), IsOpen v ‚àß c ‚äÜ v ‚àß closure v ‚äÜ u ‚àß P c v ‚àß P (closure v) u := by
  obtain ‚ü®x, uc, uu, s, huc, ucu, rfl, rfl, hn, hs‚ü© := Pcu
  obtain ‚ü®(ds : SetRel Œ± Œ±), hdsu, hdso, -, hdsd‚ü© := comp_open_symm_mem_uniformity_sets hs
  have ho : IsOpen (ds ‚óã uc ‚óã ds) := (hdso.relComp huc).relComp hdso
  have hsub := calc ds ‚óã (ds ‚óã uc ‚óã ds) ‚óã ds
    _ = (ds ‚óã ds) ‚óã uc ‚óã (ds ‚óã ds) := by simp [comp_assoc]
    _ ‚äÜ s ‚óã uc ‚óã s := comp_subset_comp (comp_subset_comp_left hdsd) hdsd
    _ ‚äÜ uu := hn
  have : ds.IsRefl := id_subset_iff.1 (refl_le_uniformity hdsu)
  refine ‚ü®ball x (ds ‚óã uc ‚óã ds), isOpen_ball x ho, ?_, subset_trans ?_ (ball_mono hsub x),
      ‚ü®x, uc, ds ‚óã uc ‚óã ds, ds, huc, ucu, rfl, rfl, le_rfl, hdsu‚ü©, x, ds ‚óã uc ‚óã ds, uu, ds, ho,
      mem_of_superset ucu (right_subset_comp.trans left_subset_comp), rfl, rfl, hsub, hdsu‚ü© <;>
  ¬∑ refine closure_ball_subset.trans (ball_mono ?_ x)
    rw [closure_eq_inter_uniformity]
    exact iInter‚ÇÇ_subset_of_subset ds hdsu (by simp [comp_assoc])

public instance UniformSpace.toCompletelyRegularSpace : CompletelyRegularSpace Œ± where
  completely_regular x K hK hx :=
    have ‚ü®O, hOu, hOo, hbO‚ü© := isOpen_iff_isOpen_ball_subset.mp hK.isOpen_compl x hx
    have ‚ü®(u3 : SetRel Œ± Œ±), hu3u, _, hu3O‚ü© := comp_comp_symm_mem_uniformity_sets hOu
    have hu3O := ((comp_subset_comp_left (comp_subset_comp_right interior_subset))).trans hu3O
    let c : Urysohns.CU P := {
      C := closure (ball x (interior u3))
      U := ball x O
      closed_C := isClosed_closure
      open_U := isOpen_ball x hOo
      subset := closure_ball_subset.trans <| (ball_mono ¬∑ x) <| by
        simp_rw [closure_eq_inter_uniformity, ‚Üê comp_assoc]
        exact (iInter‚ÇÇ_subset u3 hu3u).trans hu3O
      hP _ Pcu _ _ := urysohns_main Pcu
      P_C_U := ‚ü®x, interior u3, O, u3,
        isOpen_interior, interior_mem_uniformity hu3u, rfl, rfl, hu3O, hu3u‚ü©
    }
    ‚ü®fun x ‚Ü¶ ‚ü®c.lim x, c.lim_mem_Icc x‚ü©, c.continuous_lim.subtype_mk c.lim_mem_Icc,
      Subtype.ext (c.lim_of_mem_C x <| subset_closure (refl_mem_uniformity <|
        interior_mem_uniformity hu3u)), fun y hy ‚Ü¶ Subtype.ext (c.lim_of_notMem_U y (hbO ¬∑ hy))‚ü©

end UniformSpace

section TopologicalSpace
variable [t : TopologicalSpace Œ±]

public theorem CompletelyRegularSpace.of_exists_uniformSpace
    (h : ‚àÉ u : UniformSpace Œ±, u.toTopologicalSpace = t) :
    CompletelyRegularSpace Œ± := by
  obtain ‚ü®u, rfl‚ü© := h
  infer_instance

public theorem CompletelyRegularSpace.exists_uniformSpace [CompletelyRegularSpace Œ±] :
    ‚àÉ u : UniformSpace Œ±, u.toTopologicalSpace = t :=
  ‚ü®uniformSpaceOfCompactR1.comap stoneCechUnit, isInducing_stoneCechUnit.eq_induced.symm‚ü©

public theorem completelyRegularSpace_iff_exists_uniformSpace :
    CompletelyRegularSpace Œ± ‚Üî ‚àÉ u : UniformSpace Œ±, u.toTopologicalSpace = t :=
  ‚ü®(¬∑.exists_uniformSpace), .of_exists_uniformSpace‚ü©

end TopologicalSpace
