/-
Copyright (c) 2025 Aaron Liu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Aaron Liu
-/
module

public import Mathlib.Topology.Separation.CompletelyRegular

import Mathlib.Topology.UniformSpace.OfCompactT2
import Mathlib.Topology.UrysohnsLemma

/-!
# Uniformizable Spaces

A topological space is uniformizable (there exists a uniformity that
generates the same topology) iff it is completely regular.

## Main Results

* `UniformSpace.toCompletelyRegularSpace`: Uniform spaces are completely regular
* `CompletelyRegularSpace.exists_uniformSpace`: Completely regular spaces are uniformizable
* `CompletelyRegularSpace.of_exists_uniformSpace`: Uniformizable spaces are completely regular
* `completelyRegularSpace_iff_exists_uniformSpace`: A space is completely regular
  iff it is uniformizable

## Implementation Details

Urysohn's lemma is reused in the proof of `UniformSpace.completelyRegularSpace`.

## References

* <https://www.math.wm.edu/~vinroot/PadicGroups/519probset1.pdf>
-/

variable {Î± : Type*}

open Filter Set Uniformity SetRel

section UniformSpace
variable [UniformSpace Î±]

/-- To construct a real-valued function separating a point `x` from a closed set in a uniform
space, we recursively construct pairs of a closed set `c` contained in an open set `u`
satisfying the following predicate: the closed set is the closure of the ball centered
at `x` associated to some open entourage `uc`, the open set is the ball centered at `x`
associated to some entourage `uu`, such that `uc` and `uu` are separated by some entourage `s`
in the sense that the composition `s â—‹ uc â—‹ s` is contained in `uu`. -/
def P (c : Set Î±) (u : Set Î±) :=
  âˆƒ (x : Î±) (uc uu s : SetRel Î± Î±),
    IsOpen uc âˆ§ uc âˆˆ ğ“¤ Î± âˆ§ c = closure (Prod.mk x â»Â¹' uc) âˆ§
    u = .mk x â»Â¹' uu âˆ§ s â—‹ uc â—‹ s âŠ† uu âˆ§ s âˆˆ ğ“¤ Î±

/-- Given a pair consisting of a closed set `c` contained in an open set `u` satisfying the
predicate `P`, it is always possible to refine it to two pairs `c âŠ† v` and `closure v âŠ† u`
satisfying `P`. We can then use the general `Urysohns.CU` construction to obtain the
desired real-valued function. -/
theorem urysohns_main {c u : Set Î±} (Pcu : P c u) :
    âˆƒ (v : Set Î±), IsOpen v âˆ§ c âŠ† v âˆ§ closure v âŠ† u âˆ§ P c v âˆ§ P (closure v) u := by
  obtain âŸ¨x, uc, uu, s, huc, ucu, rfl, rfl, hn, hsâŸ© := Pcu
  obtain âŸ¨(ds : SetRel Î± Î±), hdsu, hdso, -, hdsdâŸ© := comp_open_symm_mem_uniformity_sets hs
  have ho : IsOpen (ds â—‹ uc â—‹ ds) := (hdso.relComp huc).relComp hdso
  use .mk x â»Â¹' (ds â—‹ uc â—‹ ds), ho.preimage (.prodMk_right x)
  have hsub := calc ds â—‹ (ds â—‹ uc â—‹ ds) â—‹ ds
    _ = (ds â—‹ ds) â—‹ uc â—‹ (ds â—‹ ds) := by simp [comp_assoc]
    _ âŠ† s â—‹ uc â—‹ s := comp_subset_comp (comp_subset_comp_left hdsd) hdsd
  constructor
  Â· refine ((Continuous.prodMk_right x).closure_preimage_subset _).trans (preimage_mono ?_)
    rw [closure_eq_inter_uniformity, comp_assoc]
    exact iInterâ‚‚_subset ds hdsu
  constructor
  Â· refine ((Continuous.prodMk_right x).closure_preimage_subset _).trans (preimage_mono ?_)
    rw [closure_eq_inter_uniformity]
    exact hn.trans' <| iInterâ‚‚_subset_of_subset ds hdsu <| (comp_assoc ..).symm.trans_subset hsub
  have : ds.IsRefl := id_subset_iff.1 (refl_le_uniformity hdsu)
  exact âŸ¨âŸ¨x, uc, ds â—‹ uc â—‹ ds, ds, huc, ucu, rfl, rfl, le_rfl, hdsuâŸ©, x, ds â—‹ uc â—‹ ds, uu, ds, ho,
    mem_of_superset ucu (right_subset_comp.trans left_subset_comp), rfl, rfl, hsub.trans hn, hdsuâŸ©

public instance UniformSpace.toCompletelyRegularSpace : CompletelyRegularSpace Î± where
  completely_regular x K hK hx :=
    have âŸ¨O, hOu, hOo, hbOâŸ© := isOpen_iff_isOpen_ball_subset.mp hK.isOpen_compl x hx
    have âŸ¨(u3 : SetRel Î± Î±), hu3u, _, hu3CâŸ© := comp_comp_symm_mem_uniformity_sets hOu
    have := ((comp_subset_comp_left (comp_subset_comp_right interior_subset))).trans hu3C
    let c : Urysohns.CU P :=
    { C := closure (.mk x â»Â¹' (interior u3))
      U := .mk x â»Â¹' O
      closed_C := isClosed_closure
      open_U := hOo.preimage (.prodMk_right x)
      subset := ((Continuous.prodMk_right x).closure_preimage_subset _).trans <| preimage_mono <| by
        simp_rw [closure_eq_inter_uniformity, â† comp_assoc]
        exact (iInterâ‚‚_subset u3 hu3u).trans this
      hP _ Pcu _ _ := urysohns_main Pcu
      P_C_U := âŸ¨x, _, O, u3, isOpen_interior, interior_mem_uniformity hu3u, rfl, rfl, this, hu3uâŸ© }
    âŸ¨fun x â†¦ âŸ¨c.lim x, c.lim_mem_Icc xâŸ©, c.continuous_lim.subtype_mk c.lim_mem_Icc,
      Subtype.ext (c.lim_of_mem_C x <| subset_closure (refl_mem_uniformity <|
        interior_mem_uniformity hu3u)), fun y hy â†¦ Subtype.ext (c.lim_of_notMem_U y (hbO Â· hy))âŸ©

end UniformSpace

section TopologicalSpace
variable [t : TopologicalSpace Î±]

public theorem CompletelyRegularSpace.of_exists_uniformSpace
    (h : âˆƒ u : UniformSpace Î±, u.toTopologicalSpace = t) :
    CompletelyRegularSpace Î± := by
  obtain âŸ¨u, rflâŸ© := h
  infer_instance

public theorem CompletelyRegularSpace.exists_uniformSpace [CompletelyRegularSpace Î±] :
    âˆƒ u : UniformSpace Î±, u.toTopologicalSpace = t :=
  âŸ¨uniformSpaceOfCompactR1.comap stoneCechUnit, isInducing_stoneCechUnit.eq_induced.symmâŸ©

public theorem completelyRegularSpace_iff_exists_uniformSpace :
    CompletelyRegularSpace Î± â†” âˆƒ u : UniformSpace Î±, u.toTopologicalSpace = t :=
  âŸ¨(Â·.exists_uniformSpace), .of_exists_uniformSpaceâŸ©

end TopologicalSpace
