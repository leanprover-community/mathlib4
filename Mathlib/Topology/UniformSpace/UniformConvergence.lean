/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Topology.UniformSpace.Basic
import Mathlib.Topology.UniformSpace.Cauchy

/-!
# Uniform convergence

A sequence of functions `Fâ‚™` (with values in a metric space) converges uniformly on a set `s` to a
function `f` if, for all `Îµ > 0`, for all large enough `n`, one has for all `y âˆˆ s` the inequality
`dist (f y, Fâ‚™ y) < Îµ`. Under uniform convergence, many properties of the `Fâ‚™` pass to the limit,
most notably continuity. We prove this in the file, defining the notion of uniform convergence
in the more general setting of uniform spaces, and with respect to an arbitrary indexing set
endowed with a filter (instead of just `â„•` with `atTop`).

## Main results

Let `Î±` be a topological space, `Î²` a uniform space, `Fâ‚™` and `f` be functions from `Î±` to `Î²`
(where the index `n` belongs to an indexing type `Î¹` endowed with a filter `p`).

* `TendstoUniformlyOn F f p s`: the fact that `Fâ‚™` converges uniformly to `f` on `s`. This means
  that, for any entourage `u` of the diagonal, for large enough `n` (with respect to `p`), one has
  `(f y, Fâ‚™ y) âˆˆ u` for all `y âˆˆ s`.
* `TendstoUniformly F f p`: same notion with `s = univ`.
* `TendstoUniformlyOn.continuousOn`: a uniform limit on a set of functions which are continuous
  on this set is itself continuous on this set.
* `TendstoUniformly.continuous`: a uniform limit of continuous functions is continuous.
* `TendstoUniformlyOn.tendsto_comp`: If `Fâ‚™` tends uniformly to `f` on a set `s`, and `gâ‚™` tends
  to `x` within `s`, then `Fâ‚™ gâ‚™` tends to `f x` if `f` is continuous at `x` within `s`.
* `TendstoUniformly.tendsto_comp`: If `Fâ‚™` tends uniformly to `f`, and `gâ‚™` tends to `x`, then
  `Fâ‚™ gâ‚™` tends to `f x`.

We also define notions where the convergence is locally uniform, called
`TendstoLocallyUniformlyOn F f p s` and `TendstoLocallyUniformly F f p`. The previous theorems
all have corresponding versions under locally uniform convergence.

Finally, we introduce the notion of a uniform Cauchy sequence, which is to uniform
convergence what a Cauchy sequence is to the usual notion of convergence.

## Implementation notes

We derive most of our initial results from an auxiliary definition `TendstoUniformlyOnFilter`.
This definition in and of itself can sometimes be useful, e.g., when studying the local behavior
of the `Fâ‚™` near a point, which would typically look like `TendstoUniformlyOnFilter F f p (ğ“ x)`.
Still, while this may be the "correct" definition (see
`tendstoUniformlyOn_iff_tendstoUniformlyOnFilter`), it is somewhat unwieldy to work with in
practice. Thus, we provide the more traditional definition in `TendstoUniformlyOn`.

Most results hold under weaker assumptions of locally uniform approximation. In a first section,
we prove the results under these weaker assumptions. Then, we derive the results on uniform
convergence from them.

## Tags

Uniform limit, uniform convergence, tends uniformly to
 -/


noncomputable section

open Topology Uniformity Filter Set Uniform

universe u v w x
variable {Î± : Type u} {Î² : Type v} {Î³ : Type w} {Î¹ : Type x} [UniformSpace Î²]
variable {F : Î¹ â†’ Î± â†’ Î²} {f : Î± â†’ Î²} {s s' : Set Î±} {x : Î±} {p : Filter Î¹} {p' : Filter Î±}
  {g : Î¹ â†’ Î±}

/-!
### Different notions of uniform convergence

We define uniform convergence and locally uniform convergence, on a set or in the whole space.
-/


/-- A sequence of functions `Fâ‚™` converges uniformly on a filter `p'` to a limiting function `f`
with respect to the filter `p` if, for any entourage of the diagonal `u`, one has
`p Ã—Ë¢ p'`-eventually `(f x, Fâ‚™ x) âˆˆ u`. -/
def TendstoUniformlyOnFilter (F : Î¹ â†’ Î± â†’ Î²) (f : Î± â†’ Î²) (p : Filter Î¹) (p' : Filter Î±) :=
  âˆ€ u âˆˆ ğ“¤ Î², âˆ€á¶  n : Î¹ Ã— Î± in p Ã—Ë¢ p', (f n.snd, F n.fst n.snd) âˆˆ u

/--
A sequence of functions `Fâ‚™` converges uniformly on a filter `p'` to a limiting function `f` w.r.t.
filter `p` iff the function `(n, x) â†¦ (f x, Fâ‚™ x)` converges along `p Ã—Ë¢ p'` to the uniformity.
In other words: one knows nothing about the behavior of `x` in this limit besides it being in `p'`.
-/
theorem tendstoUniformlyOnFilter_iff_tendsto :
    TendstoUniformlyOnFilter F f p p' â†”
      Tendsto (fun q : Î¹ Ã— Î± => (f q.2, F q.1 q.2)) (p Ã—Ë¢ p') (ğ“¤ Î²) :=
  Iff.rfl

/-- A sequence of functions `Fâ‚™` converges uniformly on a set `s` to a limiting function `f` with
respect to the filter `p` if, for any entourage of the diagonal `u`, one has `p`-eventually
`(f x, Fâ‚™ x) âˆˆ u` for all `x âˆˆ s`. -/
def TendstoUniformlyOn (F : Î¹ â†’ Î± â†’ Î²) (f : Î± â†’ Î²) (p : Filter Î¹) (s : Set Î±) :=
  âˆ€ u âˆˆ ğ“¤ Î², âˆ€á¶  n in p, âˆ€ x : Î±, x âˆˆ s â†’ (f x, F n x) âˆˆ u

theorem tendstoUniformlyOn_iff_tendstoUniformlyOnFilter :
    TendstoUniformlyOn F f p s â†” TendstoUniformlyOnFilter F f p (ğ“Ÿ s) := by
  simp only [TendstoUniformlyOn, TendstoUniformlyOnFilter]
  apply forallâ‚‚_congr
  simp_rw [eventually_prod_principal_iff]
  simp


alias âŸ¨TendstoUniformlyOn.tendstoUniformlyOnFilter, TendstoUniformlyOnFilter.tendstoUniformlyOnâŸ© :=
  tendstoUniformlyOn_iff_tendstoUniformlyOnFilter

/-- A sequence of functions `Fâ‚™` converges uniformly on a set `s` to a limiting function `f` w.r.t.
filter `p` iff the function `(n, x) â†¦ (f x, Fâ‚™ x)` converges along `p Ã—Ë¢ ğ“Ÿ s` to the uniformity.
In other words: one knows nothing about the behavior of `x` in this limit besides it being in `s`.
-/
theorem tendstoUniformlyOn_iff_tendsto {F : Î¹ â†’ Î± â†’ Î²} {f : Î± â†’ Î²} {p : Filter Î¹} {s : Set Î±} :
    TendstoUniformlyOn F f p s â†”
    Tendsto (fun q : Î¹ Ã— Î± => (f q.2, F q.1 q.2)) (p Ã—Ë¢ ğ“Ÿ s) (ğ“¤ Î²) := by
  simp [tendstoUniformlyOn_iff_tendstoUniformlyOnFilter, tendstoUniformlyOnFilter_iff_tendsto]

/-- A sequence of functions `Fâ‚™` converges uniformly to a limiting function `f` with respect to a
filter `p` if, for any entourage of the diagonal `u`, one has `p`-eventually
`(f x, Fâ‚™ x) âˆˆ u` for all `x`. -/
def TendstoUniformly (F : Î¹ â†’ Î± â†’ Î²) (f : Î± â†’ Î²) (p : Filter Î¹) :=
  âˆ€ u âˆˆ ğ“¤ Î², âˆ€á¶  n in p, âˆ€ x : Î±, (f x, F n x) âˆˆ u

-- Porting note: moved from below
theorem tendstoUniformlyOn_univ : TendstoUniformlyOn F f p univ â†” TendstoUniformly F f p := by
  simp [TendstoUniformlyOn, TendstoUniformly]

theorem tendstoUniformly_iff_tendstoUniformlyOnFilter :
    TendstoUniformly F f p â†” TendstoUniformlyOnFilter F f p âŠ¤ := by
  rw [â† tendstoUniformlyOn_univ, tendstoUniformlyOn_iff_tendstoUniformlyOnFilter, principal_univ]

theorem TendstoUniformly.tendstoUniformlyOnFilter (h : TendstoUniformly F f p) :
    TendstoUniformlyOnFilter F f p âŠ¤ := by rwa [â† tendstoUniformly_iff_tendstoUniformlyOnFilter]

theorem tendstoUniformlyOn_iff_tendstoUniformly_comp_coe :
    TendstoUniformlyOn F f p s â†” TendstoUniformly (fun i (x : s) => F i x) (f âˆ˜ (â†‘)) p :=
  forallâ‚‚_congr fun u _ => by simp

/-- A sequence of functions `Fâ‚™` converges uniformly to a limiting function `f` w.r.t.
filter `p` iff the function `(n, x) â†¦ (f x, Fâ‚™ x)` converges along `p Ã—Ë¢ âŠ¤` to the uniformity.
In other words: one knows nothing about the behavior of `x` in this limit.
-/
theorem tendstoUniformly_iff_tendsto {F : Î¹ â†’ Î± â†’ Î²} {f : Î± â†’ Î²} {p : Filter Î¹} :
    TendstoUniformly F f p â†” Tendsto (fun q : Î¹ Ã— Î± => (f q.2, F q.1 q.2)) (p Ã—Ë¢ âŠ¤) (ğ“¤ Î²) := by
  simp [tendstoUniformly_iff_tendstoUniformlyOnFilter, tendstoUniformlyOnFilter_iff_tendsto]

/-- Uniform converence implies pointwise convergence. -/
theorem TendstoUniformlyOnFilter.tendsto_at (h : TendstoUniformlyOnFilter F f p p')
    (hx : ğ“Ÿ {x} â‰¤ p') : Tendsto (fun n => F n x) p <| ğ“ (f x) := by
  refine Uniform.tendsto_nhds_right.mpr fun u hu => mem_map.mpr ?_
  filter_upwards [(h u hu).curry]
  intro i h
  simpa using h.filter_mono hx

/-- Uniform converence implies pointwise convergence. -/
theorem TendstoUniformlyOn.tendsto_at (h : TendstoUniformlyOn F f p s) {x : Î±} (hx : x âˆˆ s) :
    Tendsto (fun n => F n x) p <| ğ“ (f x) :=
  h.tendstoUniformlyOnFilter.tendsto_at
    (le_principal_iff.mpr <| mem_principal.mpr <| singleton_subset_iff.mpr <| hx)

/-- Uniform converence implies pointwise convergence. -/
theorem TendstoUniformly.tendsto_at (h : TendstoUniformly F f p) (x : Î±) :
    Tendsto (fun n => F n x) p <| ğ“ (f x) :=
  h.tendstoUniformlyOnFilter.tendsto_at le_top

-- Porting note: tendstoUniformlyOn_univ moved up

theorem TendstoUniformlyOnFilter.mono_left {p'' : Filter Î¹} (h : TendstoUniformlyOnFilter F f p p')
    (hp : p'' â‰¤ p) : TendstoUniformlyOnFilter F f p'' p' := fun u hu =>
  (h u hu).filter_mono (p'.prod_mono_left hp)

theorem TendstoUniformlyOnFilter.mono_right {p'' : Filter Î±} (h : TendstoUniformlyOnFilter F f p p')
    (hp : p'' â‰¤ p') : TendstoUniformlyOnFilter F f p p'' := fun u hu =>
  (h u hu).filter_mono (p.prod_mono_right hp)

theorem TendstoUniformlyOn.mono {s' : Set Î±} (h : TendstoUniformlyOn F f p s) (h' : s' âŠ† s) :
    TendstoUniformlyOn F f p s' :=
  tendstoUniformlyOn_iff_tendstoUniformlyOnFilter.mpr
    (h.tendstoUniformlyOnFilter.mono_right (le_principal_iff.mpr <| mem_principal.mpr h'))

theorem TendstoUniformlyOnFilter.congr {F' : Î¹ â†’ Î± â†’ Î²} (hf : TendstoUniformlyOnFilter F f p p')
    (hff' : âˆ€á¶  n : Î¹ Ã— Î± in p Ã—Ë¢ p', F n.fst n.snd = F' n.fst n.snd) :
    TendstoUniformlyOnFilter F' f p p' := by
  refine fun u hu => ((hf u hu).and hff').mono fun n h => ?_
  rw [â† h.right]
  exact h.left

theorem TendstoUniformlyOn.congr {F' : Î¹ â†’ Î± â†’ Î²} (hf : TendstoUniformlyOn F f p s)
    (hff' : âˆ€á¶  n in p, Set.EqOn (F n) (F' n) s) : TendstoUniformlyOn F' f p s := by
  rw [tendstoUniformlyOn_iff_tendstoUniformlyOnFilter] at hf âŠ¢
  refine hf.congr ?_
  rw [eventually_iff] at hff' âŠ¢
  simp only [Set.EqOn] at hff'
  simp only [mem_prod_principal, hff', mem_setOf_eq]

lemma tendstoUniformly_congr {F F' : Î¹ â†’ Î± â†’ Î²} {f : Î± â†’ Î²} (hF : F =á¶ [p] F') :
    TendstoUniformly F f p â†” TendstoUniformly F' f p := by
  simp_rw [â† tendstoUniformlyOn_univ] at *
  have HF := EventuallyEq.exists_mem hF
  exact âŸ¨fun h => h.congr (by aesop), fun h => h.congr (by simp_rw [eqOn_comm]; aesop)âŸ©

theorem TendstoUniformlyOn.congr_right {g : Î± â†’ Î²} (hf : TendstoUniformlyOn F f p s)
    (hfg : EqOn f g s) : TendstoUniformlyOn F g p s := fun u hu => by
  filter_upwards [hf u hu] with i hi a ha using hfg ha â–¸ hi a ha

protected theorem TendstoUniformly.tendstoUniformlyOn (h : TendstoUniformly F f p) :
    TendstoUniformlyOn F f p s :=
  (tendstoUniformlyOn_univ.2 h).mono (subset_univ s)

/-- Composing on the right by a function preserves uniform convergence on a filter -/
theorem TendstoUniformlyOnFilter.comp (h : TendstoUniformlyOnFilter F f p p') (g : Î³ â†’ Î±) :
    TendstoUniformlyOnFilter (fun n => F n âˆ˜ g) (f âˆ˜ g) p (p'.comap g) := by
  rw [tendstoUniformlyOnFilter_iff_tendsto] at h âŠ¢
  exact h.comp (tendsto_id.prod_map tendsto_comap)

/-- Composing on the right by a function preserves uniform convergence on a set -/
theorem TendstoUniformlyOn.comp (h : TendstoUniformlyOn F f p s) (g : Î³ â†’ Î±) :
    TendstoUniformlyOn (fun n => F n âˆ˜ g) (f âˆ˜ g) p (g â»Â¹' s) := by
  rw [tendstoUniformlyOn_iff_tendstoUniformlyOnFilter] at h âŠ¢
  simpa [TendstoUniformlyOn, comap_principal] using TendstoUniformlyOnFilter.comp h g

/-- Composing on the right by a function preserves uniform convergence -/
theorem TendstoUniformly.comp (h : TendstoUniformly F f p) (g : Î³ â†’ Î±) :
    TendstoUniformly (fun n => F n âˆ˜ g) (f âˆ˜ g) p := by
  rw [tendstoUniformly_iff_tendstoUniformlyOnFilter] at h âŠ¢
  simpa [principal_univ, comap_principal] using h.comp g

/-- Composing on the left by a uniformly continuous function preserves
  uniform convergence on a filter -/
theorem UniformContinuous.comp_tendstoUniformlyOnFilter [UniformSpace Î³] {g : Î² â†’ Î³}
    (hg : UniformContinuous g) (h : TendstoUniformlyOnFilter F f p p') :
    TendstoUniformlyOnFilter (fun i => g âˆ˜ F i) (g âˆ˜ f) p p' := fun _u hu => h _ (hg hu)

/-- Composing on the left by a uniformly continuous function preserves
  uniform convergence on a set -/
theorem UniformContinuous.comp_tendstoUniformlyOn [UniformSpace Î³] {g : Î² â†’ Î³}
    (hg : UniformContinuous g) (h : TendstoUniformlyOn F f p s) :
    TendstoUniformlyOn (fun i => g âˆ˜ F i) (g âˆ˜ f) p s := fun _u hu => h _ (hg hu)

/-- Composing on the left by a uniformly continuous function preserves uniform convergence -/
theorem UniformContinuous.comp_tendstoUniformly [UniformSpace Î³] {g : Î² â†’ Î³}
    (hg : UniformContinuous g) (h : TendstoUniformly F f p) :
    TendstoUniformly (fun i => g âˆ˜ F i) (g âˆ˜ f) p := fun _u hu => h _ (hg hu)

theorem TendstoUniformlyOnFilter.prod_map {Î¹' Î±' Î²' : Type*} [UniformSpace Î²'] {F' : Î¹' â†’ Î±' â†’ Î²'}
    {f' : Î±' â†’ Î²'} {q : Filter Î¹'} {q' : Filter Î±'} (h : TendstoUniformlyOnFilter F f p p')
    (h' : TendstoUniformlyOnFilter F' f' q q') :
    TendstoUniformlyOnFilter (fun i : Î¹ Ã— Î¹' => Prod.map (F i.1) (F' i.2)) (Prod.map f f')
      (p Ã—Ë¢ q) (p' Ã—Ë¢ q') := by
  rw [tendstoUniformlyOnFilter_iff_tendsto] at h h' âŠ¢
  rw [uniformity_prod_eq_comap_prod, tendsto_comap_iff, â† map_swap4_prod, tendsto_map'_iff]
  convert h.prod_map h' -- seems to be faster than `exact` here

theorem TendstoUniformlyOn.prod_map {Î¹' Î±' Î²' : Type*} [UniformSpace Î²'] {F' : Î¹' â†’ Î±' â†’ Î²'}
    {f' : Î±' â†’ Î²'} {p' : Filter Î¹'} {s' : Set Î±'} (h : TendstoUniformlyOn F f p s)
    (h' : TendstoUniformlyOn F' f' p' s') :
    TendstoUniformlyOn (fun i : Î¹ Ã— Î¹' => Prod.map (F i.1) (F' i.2)) (Prod.map f f') (p Ã—Ë¢ p')
      (s Ã—Ë¢ s') := by
  rw [tendstoUniformlyOn_iff_tendstoUniformlyOnFilter] at h h' âŠ¢
  simpa only [prod_principal_principal] using h.prod_map h'

theorem TendstoUniformly.prod_map {Î¹' Î±' Î²' : Type*} [UniformSpace Î²'] {F' : Î¹' â†’ Î±' â†’ Î²'}
    {f' : Î±' â†’ Î²'} {p' : Filter Î¹'} (h : TendstoUniformly F f p) (h' : TendstoUniformly F' f' p') :
    TendstoUniformly (fun i : Î¹ Ã— Î¹' => Prod.map (F i.1) (F' i.2)) (Prod.map f f') (p Ã—Ë¢ p') := by
  rw [â† tendstoUniformlyOn_univ, â† univ_prod_univ] at *
  exact h.prod_map h'

theorem TendstoUniformlyOnFilter.prod {Î¹' Î²' : Type*} [UniformSpace Î²'] {F' : Î¹' â†’ Î± â†’ Î²'}
    {f' : Î± â†’ Î²'} {q : Filter Î¹'} (h : TendstoUniformlyOnFilter F f p p')
    (h' : TendstoUniformlyOnFilter F' f' q p') :
    TendstoUniformlyOnFilter (fun (i : Î¹ Ã— Î¹') a => (F i.1 a, F' i.2 a)) (fun a => (f a, f' a))
      (p Ã—Ë¢ q) p' :=
  fun u hu => ((h.prod_map h') u hu).diag_of_prod_right

theorem TendstoUniformlyOn.prod {Î¹' Î²' : Type*} [UniformSpace Î²'] {F' : Î¹' â†’ Î± â†’ Î²'} {f' : Î± â†’ Î²'}
    {p' : Filter Î¹'} (h : TendstoUniformlyOn F f p s) (h' : TendstoUniformlyOn F' f' p' s) :
    TendstoUniformlyOn (fun (i : Î¹ Ã— Î¹') a => (F i.1 a, F' i.2 a)) (fun a => (f a, f' a))
      (p.prod p') s :=
  (congr_arg _ s.inter_self).mp ((h.prod_map h').comp fun a => (a, a))

theorem TendstoUniformly.prod {Î¹' Î²' : Type*} [UniformSpace Î²'] {F' : Î¹' â†’ Î± â†’ Î²'} {f' : Î± â†’ Î²'}
    {p' : Filter Î¹'} (h : TendstoUniformly F f p) (h' : TendstoUniformly F' f' p') :
    TendstoUniformly (fun (i : Î¹ Ã— Î¹') a => (F i.1 a, F' i.2 a)) (fun a => (f a, f' a))
      (p Ã—Ë¢ p') :=
  (h.prod_map h').comp fun a => (a, a)

/-- Uniform convergence on a filter `p'` to a constant function is equivalent to convergence in
`p Ã—Ë¢ p'`. -/
theorem tendsto_prod_filter_iff {c : Î²} :
    Tendsto (â†¿F) (p Ã—Ë¢ p') (ğ“ c) â†” TendstoUniformlyOnFilter F (fun _ => c) p p' := by
  simp_rw [nhds_eq_comap_uniformity, tendsto_comap_iff]
  rfl

/-- Uniform convergence on a set `s` to a constant function is equivalent to convergence in
`p Ã—Ë¢ ğ“Ÿ s`. -/
theorem tendsto_prod_principal_iff {c : Î²} :
    Tendsto (â†¿F) (p Ã—Ë¢ ğ“Ÿ s) (ğ“ c) â†” TendstoUniformlyOn F (fun _ => c) p s := by
  rw [tendstoUniformlyOn_iff_tendstoUniformlyOnFilter]
  exact tendsto_prod_filter_iff

/-- Uniform convergence to a constant function is equivalent to convergence in `p Ã—Ë¢ âŠ¤`. -/
theorem tendsto_prod_top_iff {c : Î²} :
    Tendsto (â†¿F) (p Ã—Ë¢ âŠ¤) (ğ“ c) â†” TendstoUniformly F (fun _ => c) p := by
  rw [tendstoUniformly_iff_tendstoUniformlyOnFilter]
  exact tendsto_prod_filter_iff

/-- Uniform convergence on the empty set is vacuously true -/
theorem tendstoUniformlyOn_empty : TendstoUniformlyOn F f p âˆ… := fun u _ => by simp

/-- Uniform convergence on a singleton is equivalent to regular convergence -/
theorem tendstoUniformlyOn_singleton_iff_tendsto :
    TendstoUniformlyOn F f p {x} â†” Tendsto (fun n : Î¹ => F n x) p (ğ“ (f x)) := by
  simp_rw [tendstoUniformlyOn_iff_tendsto, Uniform.tendsto_nhds_right, tendsto_def]
  exact forallâ‚‚_congr fun u _ => by simp [mem_prod_principal, preimage]

/-- If a sequence `g` converges to some `b`, then the sequence of constant functions
`fun n â†¦ fun a â†¦ g n` converges to the constant function `fun a â†¦ b` on any set `s` -/
theorem Filter.Tendsto.tendstoUniformlyOnFilter_const {g : Î¹ â†’ Î²} {b : Î²} (hg : Tendsto g p (ğ“ b))
    (p' : Filter Î±) :
    TendstoUniformlyOnFilter (fun n : Î¹ => fun _ : Î± => g n) (fun _ : Î± => b) p p' := by
  simpa only [nhds_eq_comap_uniformity, tendsto_comap_iff] using hg.comp (tendsto_fst (g := p'))

/-- If a sequence `g` converges to some `b`, then the sequence of constant functions
`fun n â†¦ fun a â†¦ g n` converges to the constant function `fun a â†¦ b` on any set `s` -/
theorem Filter.Tendsto.tendstoUniformlyOn_const {g : Î¹ â†’ Î²} {b : Î²} (hg : Tendsto g p (ğ“ b))
    (s : Set Î±) : TendstoUniformlyOn (fun n : Î¹ => fun _ : Î± => g n) (fun _ : Î± => b) p s :=
  tendstoUniformlyOn_iff_tendstoUniformlyOnFilter.mpr (hg.tendstoUniformlyOnFilter_const (ğ“Ÿ s))

theorem UniformContinuousOn.tendstoUniformlyOn [UniformSpace Î±] [UniformSpace Î³] {x : Î±} {U : Set Î±}
    {V : Set Î²} {F : Î± â†’ Î² â†’ Î³} (hF : UniformContinuousOn (â†¿F) (U Ã—Ë¢ V)) (hU : x âˆˆ U) :
    TendstoUniformlyOn F (F x) (ğ“[U] x) V := by
  set Ï† := fun q : Î± Ã— Î² => ((x, q.2), q)
  rw [tendstoUniformlyOn_iff_tendsto]
  change Tendsto (Prod.map (â†¿F) â†¿F âˆ˜ Ï†) (ğ“[U] x Ã—Ë¢ ğ“Ÿ V) (ğ“¤ Î³)
  simp only [nhdsWithin, SProd.sprod, Filter.prod, comap_inf, inf_assoc, comap_principal,
    inf_principal]
  refine hF.comp (Tendsto.inf ?_ <| tendsto_principal_principal.2 fun x hx => âŸ¨âŸ¨hU, hx.2âŸ©, hxâŸ©)
  simp only [uniformity_prod_eq_comap_prod, tendsto_comap_iff, (Â· âˆ˜ Â·),
    nhds_eq_comap_uniformity, comap_comap]
  exact tendsto_comap.prod_mk (tendsto_diag_uniformity _ _)

theorem UniformContinuousOn.tendstoUniformly [UniformSpace Î±] [UniformSpace Î³] {x : Î±} {U : Set Î±}
    (hU : U âˆˆ ğ“ x) {F : Î± â†’ Î² â†’ Î³} (hF : UniformContinuousOn (â†¿F) (U Ã—Ë¢ (univ : Set Î²))) :
    TendstoUniformly F (F x) (ğ“ x) := by
  simpa only [tendstoUniformlyOn_univ, nhdsWithin_eq_nhds.2 hU]
    using hF.tendstoUniformlyOn (mem_of_mem_nhds hU)

theorem UniformContinuousâ‚‚.tendstoUniformly [UniformSpace Î±] [UniformSpace Î³] {f : Î± â†’ Î² â†’ Î³}
    (h : UniformContinuousâ‚‚ f) {x : Î±} : TendstoUniformly f (f x) (ğ“ x) :=
  UniformContinuousOn.tendstoUniformly univ_mem <| by rwa [univ_prod_univ, uniformContinuousOn_univ]

/-- A sequence is uniformly Cauchy if eventually all of its pairwise differences are
uniformly bounded -/
def UniformCauchySeqOnFilter (F : Î¹ â†’ Î± â†’ Î²) (p : Filter Î¹) (p' : Filter Î±) : Prop :=
  âˆ€ u âˆˆ ğ“¤ Î², âˆ€á¶  m : (Î¹ Ã— Î¹) Ã— Î± in (p Ã—Ë¢ p) Ã—Ë¢ p', (F m.fst.fst m.snd, F m.fst.snd m.snd) âˆˆ u

/-- A sequence is uniformly Cauchy if eventually all of its pairwise differences are
uniformly bounded -/
def UniformCauchySeqOn (F : Î¹ â†’ Î± â†’ Î²) (p : Filter Î¹) (s : Set Î±) : Prop :=
  âˆ€ u âˆˆ ğ“¤ Î², âˆ€á¶  m : Î¹ Ã— Î¹ in p Ã—Ë¢ p, âˆ€ x : Î±, x âˆˆ s â†’ (F m.fst x, F m.snd x) âˆˆ u

theorem uniformCauchySeqOn_iff_uniformCauchySeqOnFilter :
    UniformCauchySeqOn F p s â†” UniformCauchySeqOnFilter F p (ğ“Ÿ s) := by
  simp only [UniformCauchySeqOn, UniformCauchySeqOnFilter]
  refine forallâ‚‚_congr fun u hu => ?_
  rw [eventually_prod_principal_iff]

theorem UniformCauchySeqOn.uniformCauchySeqOnFilter (hF : UniformCauchySeqOn F p s) :
    UniformCauchySeqOnFilter F p (ğ“Ÿ s) := by rwa [â† uniformCauchySeqOn_iff_uniformCauchySeqOnFilter]

/-- A sequence that converges uniformly is also uniformly Cauchy -/
theorem TendstoUniformlyOnFilter.uniformCauchySeqOnFilter (hF : TendstoUniformlyOnFilter F f p p') :
    UniformCauchySeqOnFilter F p p' := by
  intro u hu
  rcases comp_symm_of_uniformity hu with âŸ¨t, ht, htsymm, htmemâŸ©
  have := tendsto_swap4_prod.eventually ((hF t ht).prod_mk (hF t ht))
  apply this.diag_of_prod_right.mono
  simp only [and_imp, Prod.forall]
  intro n1 n2 x hl hr
  exact Set.mem_of_mem_of_subset (prod_mk_mem_compRel (htsymm hl) hr) htmem

/-- A sequence that converges uniformly is also uniformly Cauchy -/
theorem TendstoUniformlyOn.uniformCauchySeqOn (hF : TendstoUniformlyOn F f p s) :
    UniformCauchySeqOn F p s :=
  uniformCauchySeqOn_iff_uniformCauchySeqOnFilter.mpr
    hF.tendstoUniformlyOnFilter.uniformCauchySeqOnFilter

/-- A uniformly Cauchy sequence converges uniformly to its limit -/
theorem UniformCauchySeqOnFilter.tendstoUniformlyOnFilter_of_tendsto
    (hF : UniformCauchySeqOnFilter F p p')
    (hF' : âˆ€á¶  x : Î± in p', Tendsto (fun n => F n x) p (ğ“ (f x))) :
    TendstoUniformlyOnFilter F f p p' := by
  rcases p.eq_or_neBot with rfl | _
  Â· simp only [TendstoUniformlyOnFilter, bot_prod, eventually_bot, implies_true]
  -- Proof idea: |f_n(x) - f(x)| â‰¤ |f_n(x) - f_m(x)| + |f_m(x) - f(x)|. We choose `n`
  -- so that |f_n(x) - f_m(x)| is uniformly small across `s` whenever `m â‰¥ n`. Then for
  -- a fixed `x`, we choose `m` sufficiently large such that |f_m(x) - f(x)| is small.
  intro u hu
  rcases comp_symm_of_uniformity hu with âŸ¨t, ht, htsymm, htmemâŸ©
  -- We will choose n, x, and m simultaneously. n and x come from hF. m comes from hF'
  -- But we need to promote hF' to the full product filter to use it
  have hmc : âˆ€á¶  x in (p Ã—Ë¢ p) Ã—Ë¢ p', Tendsto (fun n : Î¹ => F n x.snd) p (ğ“ (f x.snd)) := by
    rw [eventually_prod_iff]
    exact âŸ¨fun _ => True, by simp, _, hF', by simpâŸ©
  -- To apply filter operations we'll need to do some order manipulation
  rw [Filter.eventually_swap_iff]
  have := tendsto_prodAssoc.eventually (tendsto_prod_swap.eventually ((hF t ht).and hmc))
  apply this.curry.mono
  simp only [Equiv.prodAssoc_apply, eventually_and, eventually_const, Prod.snd_swap, Prod.fst_swap,
    and_imp, Prod.forall]
  -- Complete the proof
  intro x n hx hm'
  refine Set.mem_of_mem_of_subset (mem_compRel.mpr ?_) htmem
  rw [Uniform.tendsto_nhds_right] at hm'
  have := hx.and (hm' ht)
  obtain âŸ¨m, hmâŸ© := this.exists
  exact âŸ¨F m x, âŸ¨hm.2, htsymm hm.1âŸ©âŸ©

/-- A uniformly Cauchy sequence converges uniformly to its limit -/
theorem UniformCauchySeqOn.tendstoUniformlyOn_of_tendsto (hF : UniformCauchySeqOn F p s)
    (hF' : âˆ€ x : Î±, x âˆˆ s â†’ Tendsto (fun n => F n x) p (ğ“ (f x))) : TendstoUniformlyOn F f p s :=
  tendstoUniformlyOn_iff_tendstoUniformlyOnFilter.mpr
    (hF.uniformCauchySeqOnFilter.tendstoUniformlyOnFilter_of_tendsto hF')

theorem UniformCauchySeqOnFilter.mono_left {p'' : Filter Î¹} (hf : UniformCauchySeqOnFilter F p p')
    (hp : p'' â‰¤ p) : UniformCauchySeqOnFilter F p'' p' := by
  intro u hu
  have := (hf u hu).filter_mono (p'.prod_mono_left (Filter.prod_mono hp hp))
  exact this.mono (by simp)

theorem UniformCauchySeqOnFilter.mono_right {p'' : Filter Î±} (hf : UniformCauchySeqOnFilter F p p')
    (hp : p'' â‰¤ p') : UniformCauchySeqOnFilter F p p'' := fun u hu =>
  have := (hf u hu).filter_mono ((p Ã—Ë¢ p).prod_mono_right hp)
  this.mono (by simp)

theorem UniformCauchySeqOn.mono {s' : Set Î±} (hf : UniformCauchySeqOn F p s) (hss' : s' âŠ† s) :
    UniformCauchySeqOn F p s' := by
  rw [uniformCauchySeqOn_iff_uniformCauchySeqOnFilter] at hf âŠ¢
  exact hf.mono_right (le_principal_iff.mpr <| mem_principal.mpr hss')

/-- Composing on the right by a function preserves uniform Cauchy sequences -/
theorem UniformCauchySeqOnFilter.comp {Î³ : Type*} (hf : UniformCauchySeqOnFilter F p p')
    (g : Î³ â†’ Î±) : UniformCauchySeqOnFilter (fun n => F n âˆ˜ g) p (p'.comap g) := fun u hu => by
  obtain âŸ¨pa, hpa, pb, hpb, hpapbâŸ© := eventually_prod_iff.mp (hf u hu)
  rw [eventually_prod_iff]
  refine âŸ¨pa, hpa, pb âˆ˜ g, ?_, fun hx _ hy => hpapb hx hyâŸ©
  exact eventually_comap.mpr (hpb.mono fun x hx y hy => by simp only [hx, hy, Function.comp_apply])

/-- Composing on the right by a function preserves uniform Cauchy sequences -/
theorem UniformCauchySeqOn.comp {Î³ : Type*} (hf : UniformCauchySeqOn F p s) (g : Î³ â†’ Î±) :
    UniformCauchySeqOn (fun n => F n âˆ˜ g) p (g â»Â¹' s) := by
  rw [uniformCauchySeqOn_iff_uniformCauchySeqOnFilter] at hf âŠ¢
  simpa only [UniformCauchySeqOn, comap_principal] using hf.comp g

/-- Composing on the left by a uniformly continuous function preserves
uniform Cauchy sequences -/
theorem UniformContinuous.comp_uniformCauchySeqOn [UniformSpace Î³] {g : Î² â†’ Î³}
    (hg : UniformContinuous g) (hf : UniformCauchySeqOn F p s) :
    UniformCauchySeqOn (fun n => g âˆ˜ F n) p s := fun _u hu => hf _ (hg hu)

theorem UniformCauchySeqOn.prod_map {Î¹' Î±' Î²' : Type*} [UniformSpace Î²'] {F' : Î¹' â†’ Î±' â†’ Î²'}
    {p' : Filter Î¹'} {s' : Set Î±'} (h : UniformCauchySeqOn F p s)
    (h' : UniformCauchySeqOn F' p' s') :
    UniformCauchySeqOn (fun i : Î¹ Ã— Î¹' => Prod.map (F i.1) (F' i.2)) (p Ã—Ë¢ p') (s Ã—Ë¢ s') := by
  intro u hu
  rw [uniformity_prod_eq_prod, mem_map, mem_prod_iff] at hu
  obtain âŸ¨v, hv, w, hw, hvwâŸ© := hu
  simp_rw [mem_prod, and_imp, Prod.forall, Prod.map_apply]
  rw [â† Set.image_subset_iff] at hvw
  apply (tendsto_swap4_prod.eventually ((h v hv).prod_mk (h' w hw))).mono
  intro x hx a b ha hb
  exact hvw âŸ¨_, mk_mem_prod (hx.1 a ha) (hx.2 b hb), rflâŸ©

theorem UniformCauchySeqOn.prod {Î¹' Î²' : Type*} [UniformSpace Î²'] {F' : Î¹' â†’ Î± â†’ Î²'}
    {p' : Filter Î¹'} (h : UniformCauchySeqOn F p s) (h' : UniformCauchySeqOn F' p' s) :
    UniformCauchySeqOn (fun (i : Î¹ Ã— Î¹') a => (F i.fst a, F' i.snd a)) (p Ã—Ë¢ p') s :=
  (congr_arg _ s.inter_self).mp ((h.prod_map h').comp fun a => (a, a))

theorem UniformCauchySeqOn.prod' {Î²' : Type*} [UniformSpace Î²'] {F' : Î¹ â†’ Î± â†’ Î²'}
    (h : UniformCauchySeqOn F p s) (h' : UniformCauchySeqOn F' p s) :
    UniformCauchySeqOn (fun (i : Î¹) a => (F i a, F' i a)) p s := fun u hu =>
  have hh : Tendsto (fun x : Î¹ => (x, x)) p (p Ã—Ë¢ p) := tendsto_diag
  (hh.prod_map hh).eventually ((h.prod h') u hu)

/-- If a sequence of functions is uniformly Cauchy on a set, then the values at each point form
a Cauchy sequence. -/
theorem UniformCauchySeqOn.cauchy_map [hp : NeBot p] (hf : UniformCauchySeqOn F p s) (hx : x âˆˆ s) :
    Cauchy (map (fun i => F i x) p) := by
  simp only [cauchy_map_iff, hp, true_and]
  intro u hu
  rw [mem_map]
  filter_upwards [hf u hu] with p hp using hp x hx

/-- If a sequence of functions is uniformly Cauchy on a set, then the values at each point form
a Cauchy sequence.  See `UniformCauchSeqOn.cauchy_map` for the non-`atTop` case. -/
theorem UniformCauchySeqOn.cauchySeq [Nonempty Î¹] [SemilatticeSup Î¹]
    (hf : UniformCauchySeqOn F atTop s) (hx : x âˆˆ s) :
    CauchySeq fun i â†¦ F i x :=
  hf.cauchy_map (hp := atTop_neBot) hx

section SeqTendsto

theorem tendstoUniformlyOn_of_seq_tendstoUniformlyOn {l : Filter Î¹} [l.IsCountablyGenerated]
    (h : âˆ€ u : â„• â†’ Î¹, Tendsto u atTop l â†’ TendstoUniformlyOn (fun n => F (u n)) f atTop s) :
    TendstoUniformlyOn F f l s := by
  rw [tendstoUniformlyOn_iff_tendsto, tendsto_iff_seq_tendsto]
  intro u hu
  rw [tendsto_prod_iff'] at hu
  specialize h (fun n => (u n).fst) hu.1
  rw [tendstoUniformlyOn_iff_tendsto] at h
  exact h.comp (tendsto_id.prod_mk hu.2)

theorem TendstoUniformlyOn.seq_tendstoUniformlyOn {l : Filter Î¹} (h : TendstoUniformlyOn F f l s)
    (u : â„• â†’ Î¹) (hu : Tendsto u atTop l) : TendstoUniformlyOn (fun n => F (u n)) f atTop s := by
  rw [tendstoUniformlyOn_iff_tendsto] at h âŠ¢
  exact h.comp ((hu.comp tendsto_fst).prod_mk tendsto_snd)

theorem tendstoUniformlyOn_iff_seq_tendstoUniformlyOn {l : Filter Î¹} [l.IsCountablyGenerated] :
    TendstoUniformlyOn F f l s â†”
      âˆ€ u : â„• â†’ Î¹, Tendsto u atTop l â†’ TendstoUniformlyOn (fun n => F (u n)) f atTop s :=
  âŸ¨TendstoUniformlyOn.seq_tendstoUniformlyOn, tendstoUniformlyOn_of_seq_tendstoUniformlyOnâŸ©

theorem tendstoUniformly_iff_seq_tendstoUniformly {l : Filter Î¹} [l.IsCountablyGenerated] :
    TendstoUniformly F f l â†”
      âˆ€ u : â„• â†’ Î¹, Tendsto u atTop l â†’ TendstoUniformly (fun n => F (u n)) f atTop := by
  simp_rw [â† tendstoUniformlyOn_univ]
  exact tendstoUniformlyOn_iff_seq_tendstoUniformlyOn

end SeqTendsto

section

variable [NeBot p] {L : Î¹ â†’ Î²} {â„“ : Î²}

theorem TendstoUniformlyOnFilter.tendsto_of_eventually_tendsto
    (h1 : TendstoUniformlyOnFilter F f p p') (h2 : âˆ€á¶  i in p, Tendsto (F i) p' (ğ“ (L i)))
    (h3 : Tendsto L p (ğ“ â„“)) : Tendsto f p' (ğ“ â„“) := by
  rw [tendsto_nhds_left]
  intro s hs
  rw [mem_map, Set.preimage, â† eventually_iff]
  obtain âŸ¨t, ht, htsâŸ© := comp3_mem_uniformity hs
  have p1 : âˆ€á¶  i in p, (L i, â„“) âˆˆ t := tendsto_nhds_left.mp h3 ht
  have p2 : âˆ€á¶  i in p, âˆ€á¶  x in p', (F i x, L i) âˆˆ t := by
    filter_upwards [h2] with i h2 using tendsto_nhds_left.mp h2 ht
  have p3 : âˆ€á¶  i in p, âˆ€á¶  x in p', (f x, F i x) âˆˆ t := (h1 t ht).curry
  obtain âŸ¨i, p4, p5, p6âŸ© := (p1.and (p2.and p3)).exists
  filter_upwards [p5, p6] with x p5 p6 using hts âŸ¨F i x, p6, L i, p5, p4âŸ©

theorem TendstoUniformly.tendsto_of_eventually_tendsto
    (h1 : TendstoUniformly F f p) (h2 : âˆ€á¶  i in p, Tendsto (F i) p' (ğ“ (L i)))
    (h3 : Tendsto L p (ğ“ â„“)) : Tendsto f p' (ğ“ â„“) :=
  (h1.tendstoUniformlyOnFilter.mono_right le_top).tendsto_of_eventually_tendsto h2 h3

end

variable [TopologicalSpace Î±]

/-- A sequence of functions `Fâ‚™` converges locally uniformly on a set `s` to a limiting function
`f` with respect to a filter `p` if, for any entourage of the diagonal `u`, for any `x âˆˆ s`, one
has `p`-eventually `(f y, Fâ‚™ y) âˆˆ u` for all `y` in a neighborhood of `x` in `s`. -/
def TendstoLocallyUniformlyOn (F : Î¹ â†’ Î± â†’ Î²) (f : Î± â†’ Î²) (p : Filter Î¹) (s : Set Î±) :=
  âˆ€ u âˆˆ ğ“¤ Î², âˆ€ x âˆˆ s, âˆƒ t âˆˆ ğ“[s] x, âˆ€á¶  n in p, âˆ€ y âˆˆ t, (f y, F n y) âˆˆ u

/-- A sequence of functions `Fâ‚™` converges locally uniformly to a limiting function `f` with respect
to a filter `p` if, for any entourage of the diagonal `u`, for any `x`, one has `p`-eventually
`(f y, Fâ‚™ y) âˆˆ u` for all `y` in a neighborhood of `x`. -/
def TendstoLocallyUniformly (F : Î¹ â†’ Î± â†’ Î²) (f : Î± â†’ Î²) (p : Filter Î¹) :=
  âˆ€ u âˆˆ ğ“¤ Î², âˆ€ x : Î±, âˆƒ t âˆˆ ğ“ x, âˆ€á¶  n in p, âˆ€ y âˆˆ t, (f y, F n y) âˆˆ u

theorem tendstoLocallyUniformlyOn_univ :
    TendstoLocallyUniformlyOn F f p univ â†” TendstoLocallyUniformly F f p := by
  simp [TendstoLocallyUniformlyOn, TendstoLocallyUniformly, nhdsWithin_univ]

theorem tendstoLocallyUniformlyOn_iff_forall_tendsto :
    TendstoLocallyUniformlyOn F f p s â†”
      âˆ€ x âˆˆ s, Tendsto (fun y : Î¹ Ã— Î± => (f y.2, F y.1 y.2)) (p Ã—Ë¢ ğ“[s] x) (ğ“¤ Î²) :=
  forallâ‚‚_swap.trans <| forallâ‚„_congr fun _ _ _ _ => by
    rw [mem_map, mem_prod_iff_right]; rfl

nonrec theorem IsOpen.tendstoLocallyUniformlyOn_iff_forall_tendsto (hs : IsOpen s) :
    TendstoLocallyUniformlyOn F f p s â†”
      âˆ€ x âˆˆ s, Tendsto (fun y : Î¹ Ã— Î± => (f y.2, F y.1 y.2)) (p Ã—Ë¢ ğ“ x) (ğ“¤ Î²) :=
  tendstoLocallyUniformlyOn_iff_forall_tendsto.trans <| forallâ‚‚_congr fun x hx => by
    rw [hs.nhdsWithin_eq hx]

theorem tendstoLocallyUniformly_iff_forall_tendsto :
    TendstoLocallyUniformly F f p â†”
      âˆ€ x, Tendsto (fun y : Î¹ Ã— Î± => (f y.2, F y.1 y.2)) (p Ã—Ë¢ ğ“ x) (ğ“¤ Î²) := by
  simp [â† tendstoLocallyUniformlyOn_univ, isOpen_univ.tendstoLocallyUniformlyOn_iff_forall_tendsto]

theorem tendstoLocallyUniformlyOn_iff_tendstoLocallyUniformly_comp_coe :
    TendstoLocallyUniformlyOn F f p s â†”
      TendstoLocallyUniformly (fun i (x : s) => F i x) (f âˆ˜ (â†‘)) p := by
  simp only [tendstoLocallyUniformly_iff_forall_tendsto, Subtype.forall', tendsto_map'_iff,
    tendstoLocallyUniformlyOn_iff_forall_tendsto, â† map_nhds_subtype_val, prod_map_right]; rfl

protected theorem TendstoUniformlyOn.tendstoLocallyUniformlyOn (h : TendstoUniformlyOn F f p s) :
    TendstoLocallyUniformlyOn F f p s := fun u hu _ _ =>
  âŸ¨s, self_mem_nhdsWithin, by simpa using h u huâŸ©

protected theorem TendstoUniformly.tendstoLocallyUniformly (h : TendstoUniformly F f p) :
    TendstoLocallyUniformly F f p := fun u hu _ => âŸ¨univ, univ_mem, by simpa using h u huâŸ©

theorem TendstoLocallyUniformlyOn.mono (h : TendstoLocallyUniformlyOn F f p s) (h' : s' âŠ† s) :
    TendstoLocallyUniformlyOn F f p s' := by
  intro u hu x hx
  rcases h u hu x (h' hx) with âŸ¨t, ht, HâŸ©
  exact âŸ¨t, nhdsWithin_mono x h' ht, H.mono fun n => idâŸ©

-- Porting note: generalized from `Type` to `Sort`
theorem tendstoLocallyUniformlyOn_iUnion {Î¹' : Sort*} {S : Î¹' â†’ Set Î±} (hS : âˆ€ i, IsOpen (S i))
    (h : âˆ€ i, TendstoLocallyUniformlyOn F f p (S i)) :
    TendstoLocallyUniformlyOn F f p (â‹ƒ i, S i) :=
  (isOpen_iUnion hS).tendstoLocallyUniformlyOn_iff_forall_tendsto.2 fun _x hx =>
    let âŸ¨i, hiâŸ© := mem_iUnion.1 hx
    (hS i).tendstoLocallyUniformlyOn_iff_forall_tendsto.1 (h i) _ hi

theorem tendstoLocallyUniformlyOn_biUnion {s : Set Î³} {S : Î³ â†’ Set Î±} (hS : âˆ€ i âˆˆ s, IsOpen (S i))
    (h : âˆ€ i âˆˆ s, TendstoLocallyUniformlyOn F f p (S i)) :
    TendstoLocallyUniformlyOn F f p (â‹ƒ i âˆˆ s, S i) :=
  tendstoLocallyUniformlyOn_iUnion (fun i => isOpen_iUnion (hS i)) fun i =>
   tendstoLocallyUniformlyOn_iUnion (hS i) (h i)

theorem tendstoLocallyUniformlyOn_sUnion (S : Set (Set Î±)) (hS : âˆ€ s âˆˆ S, IsOpen s)
    (h : âˆ€ s âˆˆ S, TendstoLocallyUniformlyOn F f p s) : TendstoLocallyUniformlyOn F f p (â‹ƒâ‚€ S) := by
  rw [sUnion_eq_biUnion]
  exact tendstoLocallyUniformlyOn_biUnion hS h

theorem TendstoLocallyUniformlyOn.union {sâ‚ sâ‚‚ : Set Î±} (hsâ‚ : IsOpen sâ‚) (hsâ‚‚ : IsOpen sâ‚‚)
    (hâ‚ : TendstoLocallyUniformlyOn F f p sâ‚) (hâ‚‚ : TendstoLocallyUniformlyOn F f p sâ‚‚) :
    TendstoLocallyUniformlyOn F f p (sâ‚ âˆª sâ‚‚) := by
  rw [â† sUnion_pair]
  refine tendstoLocallyUniformlyOn_sUnion _ ?_ ?_ <;> simp [*]

-- Porting note: tendstoLocallyUniformlyOn_univ moved up

protected theorem TendstoLocallyUniformly.tendstoLocallyUniformlyOn
    (h : TendstoLocallyUniformly F f p) : TendstoLocallyUniformlyOn F f p s :=
  (tendstoLocallyUniformlyOn_univ.mpr h).mono (subset_univ _)

/-- On a compact space, locally uniform convergence is just uniform convergence. -/
theorem tendstoLocallyUniformly_iff_tendstoUniformly_of_compactSpace [CompactSpace Î±] :
    TendstoLocallyUniformly F f p â†” TendstoUniformly F f p := by
  refine âŸ¨fun h V hV => ?_, TendstoUniformly.tendstoLocallyUniformlyâŸ©
  choose U hU using h V hV
  obtain âŸ¨t, htâŸ© := isCompact_univ.elim_nhds_subcover' (fun k _ => U k) fun k _ => (hU k).1
  replace hU := fun x : t => (hU x).2
  rw [â† eventually_all] at hU
  refine hU.mono fun i hi x => ?_
  specialize ht (mem_univ x)
  simp only [exists_prop, mem_iUnion, SetCoe.exists, exists_and_right, Subtype.coe_mk] at ht
  obtain âŸ¨y, âŸ¨hyâ‚, hyâ‚‚âŸ©, hyâ‚ƒâŸ© := ht
  exact hi âŸ¨âŸ¨y, hyâ‚âŸ©, hyâ‚‚âŸ© x hyâ‚ƒ

/-- For a compact set `s`, locally uniform convergence on `s` is just uniform convergence on `s`. -/
theorem tendstoLocallyUniformlyOn_iff_tendstoUniformlyOn_of_compact (hs : IsCompact s) :
    TendstoLocallyUniformlyOn F f p s â†” TendstoUniformlyOn F f p s := by
  haveI : CompactSpace s := isCompact_iff_compactSpace.mp hs
  refine âŸ¨fun h => ?_, TendstoUniformlyOn.tendstoLocallyUniformlyOnâŸ©
  rwa [tendstoLocallyUniformlyOn_iff_tendstoLocallyUniformly_comp_coe,
    tendstoLocallyUniformly_iff_tendstoUniformly_of_compactSpace, â†
    tendstoUniformlyOn_iff_tendstoUniformly_comp_coe] at h

theorem TendstoLocallyUniformlyOn.comp [TopologicalSpace Î³] {t : Set Î³}
    (h : TendstoLocallyUniformlyOn F f p s) (g : Î³ â†’ Î±) (hg : MapsTo g t s)
    (cg : ContinuousOn g t) : TendstoLocallyUniformlyOn (fun n => F n âˆ˜ g) (f âˆ˜ g) p t := by
  intro u hu x hx
  rcases h u hu (g x) (hg hx) with âŸ¨a, ha, HâŸ©
  have : g â»Â¹' a âˆˆ ğ“[t] x :=
    (cg x hx).preimage_mem_nhdsWithin' (nhdsWithin_mono (g x) hg.image_subset ha)
  exact âŸ¨g â»Â¹' a, this, H.mono fun n hn y hy => hn _ hyâŸ©

theorem TendstoLocallyUniformly.comp [TopologicalSpace Î³] (h : TendstoLocallyUniformly F f p)
    (g : Î³ â†’ Î±) (cg : Continuous g) : TendstoLocallyUniformly (fun n => F n âˆ˜ g) (f âˆ˜ g) p := by
  rw [â† tendstoLocallyUniformlyOn_univ] at h âŠ¢
  rw [continuous_iff_continuousOn_univ] at cg
  exact h.comp _ (mapsTo_univ _ _) cg

theorem tendstoLocallyUniformlyOn_TFAE [LocallyCompactSpace Î±] (G : Î¹ â†’ Î± â†’ Î²) (g : Î± â†’ Î²)
    (p : Filter Î¹) (hs : IsOpen s) :
    List.TFAE [
      TendstoLocallyUniformlyOn G g p s,
      âˆ€ K, K âŠ† s â†’ IsCompact K â†’ TendstoUniformlyOn G g p K,
      âˆ€ x âˆˆ s, âˆƒ v âˆˆ ğ“[s] x, TendstoUniformlyOn G g p v] := by
  tfae_have 1 â†’ 2
  | h, K, hK1, hK2 =>
    (tendstoLocallyUniformlyOn_iff_tendstoUniformlyOn_of_compact hK2).mp (h.mono hK1)
  tfae_have 2 â†’ 3
  | h, x, hx => by
    obtain âŸ¨K, âŸ¨hK1, hK2âŸ©, hK3âŸ© := (compact_basis_nhds x).mem_iff.mp (hs.mem_nhds hx)
    exact âŸ¨K, nhdsWithin_le_nhds hK1, h K hK3 hK2âŸ©
  tfae_have 3 â†’ 1
  | h, u, hu, x, hx => by
    obtain âŸ¨v, hv1, hv2âŸ© := h x hx
    exact âŸ¨v, hv1, hv2 u huâŸ©
  tfae_finish

theorem tendstoLocallyUniformlyOn_iff_forall_isCompact [LocallyCompactSpace Î±] (hs : IsOpen s) :
    TendstoLocallyUniformlyOn F f p s â†” âˆ€ K, K âŠ† s â†’ IsCompact K â†’ TendstoUniformlyOn F f p K :=
  (tendstoLocallyUniformlyOn_TFAE F f p hs).out 0 1

lemma tendstoLocallyUniformly_iff_forall_isCompact [LocallyCompactSpace Î±]  :
    TendstoLocallyUniformly F f p â†” âˆ€ K : Set Î±, IsCompact K â†’ TendstoUniformlyOn F f p K := by
  simp only [â† tendstoLocallyUniformlyOn_univ,
    tendstoLocallyUniformlyOn_iff_forall_isCompact isOpen_univ, Set.subset_univ, forall_true_left]

theorem tendstoLocallyUniformlyOn_iff_filter :
    TendstoLocallyUniformlyOn F f p s â†” âˆ€ x âˆˆ s, TendstoUniformlyOnFilter F f p (ğ“[s] x) := by
  simp only [TendstoUniformlyOnFilter, eventually_prod_iff]
  constructor
  Â· rintro h x hx u hu
    obtain âŸ¨s, hs1, hs2âŸ© := h u hu x hx
    exact âŸ¨_, hs2, _, eventually_of_mem hs1 fun x => id, fun hi y hy => hi y hyâŸ©
  Â· rintro h u hu x hx
    obtain âŸ¨pa, hpa, pb, hpb, hâŸ© := h x hx u hu
    exact âŸ¨pb, hpb, eventually_of_mem hpa fun i hi y hy => h hi hyâŸ©

theorem tendstoLocallyUniformly_iff_filter :
    TendstoLocallyUniformly F f p â†” âˆ€ x, TendstoUniformlyOnFilter F f p (ğ“ x) := by
  simpa [â† tendstoLocallyUniformlyOn_univ, â† nhdsWithin_univ] using
    @tendstoLocallyUniformlyOn_iff_filter _ _ _ _ F f univ p _

theorem TendstoLocallyUniformlyOn.tendsto_at (hf : TendstoLocallyUniformlyOn F f p s) {a : Î±}
    (ha : a âˆˆ s) : Tendsto (fun i => F i a) p (ğ“ (f a)) := by
  refine ((tendstoLocallyUniformlyOn_iff_filter.mp hf) a ha).tendsto_at ?_
  simpa only [Filter.principal_singleton] using pure_le_nhdsWithin ha

theorem TendstoLocallyUniformlyOn.unique [p.NeBot] [T2Space Î²] {g : Î± â†’ Î²}
    (hf : TendstoLocallyUniformlyOn F f p s) (hg : TendstoLocallyUniformlyOn F g p s) :
    s.EqOn f g := fun _a ha => tendsto_nhds_unique (hf.tendsto_at ha) (hg.tendsto_at ha)

theorem TendstoLocallyUniformlyOn.congr {G : Î¹ â†’ Î± â†’ Î²} (hf : TendstoLocallyUniformlyOn F f p s)
    (hg : âˆ€ n, s.EqOn (F n) (G n)) : TendstoLocallyUniformlyOn G f p s := by
  rintro u hu x hx
  obtain âŸ¨t, ht, hâŸ© := hf u hu x hx
  refine âŸ¨s âˆ© t, inter_mem self_mem_nhdsWithin ht, ?_âŸ©
  filter_upwards [h] with i hi y hy using hg i hy.1 â–¸ hi y hy.2

theorem TendstoLocallyUniformlyOn.congr_right {g : Î± â†’ Î²} (hf : TendstoLocallyUniformlyOn F f p s)
    (hg : s.EqOn f g) : TendstoLocallyUniformlyOn F g p s := by
  rintro u hu x hx
  obtain âŸ¨t, ht, hâŸ© := hf u hu x hx
  refine âŸ¨s âˆ© t, inter_mem self_mem_nhdsWithin ht, ?_âŸ©
  filter_upwards [h] with i hi y hy using hg hy.1 â–¸ hi y hy.2

/-!
### Uniform approximation

In this section, we give lemmas ensuring that a function is continuous if it can be approximated
uniformly by continuous functions. We give various versions, within a set or the whole space, at
a single point or at all points, with locally uniform approximation or uniform approximation. All
the statements are derived from a statement about locally uniform approximation within a set at
a point, called `continuousWithinAt_of_locally_uniform_approx_of_continuousWithinAt`. -/


/-- A function which can be locally uniformly approximated by functions which are continuous
within a set at a point is continuous within this set at this point. -/
theorem continuousWithinAt_of_locally_uniform_approx_of_continuousWithinAt (hx : x âˆˆ s)
    (L : âˆ€ u âˆˆ ğ“¤ Î², âˆƒ t âˆˆ ğ“[s] x, âˆƒ F : Î± â†’ Î², ContinuousWithinAt F s x âˆ§ âˆ€ y âˆˆ t, (f y, F y) âˆˆ u) :
    ContinuousWithinAt f s x := by
  refine Uniform.continuousWithinAt_iff'_left.2 fun uâ‚€ huâ‚€ => ?_
  obtain âŸ¨uâ‚, hâ‚, uâ‚â‚€âŸ© : âˆƒ u âˆˆ ğ“¤ Î², u â—‹ u âŠ† uâ‚€ := comp_mem_uniformity_sets huâ‚€
  obtain âŸ¨uâ‚‚, hâ‚‚, hsymm, uâ‚‚â‚âŸ© : âˆƒ u âˆˆ ğ“¤ Î², (âˆ€ {a b}, (a, b) âˆˆ u â†’ (b, a) âˆˆ u) âˆ§ u â—‹ u âŠ† uâ‚ :=
    comp_symm_of_uniformity hâ‚
  rcases L uâ‚‚ hâ‚‚ with âŸ¨t, tx, F, hFc, hFâŸ©
  have A : âˆ€á¶  y in ğ“[s] x, (f y, F y) âˆˆ uâ‚‚ := Eventually.mono tx hF
  have B : âˆ€á¶  y in ğ“[s] x, (F y, F x) âˆˆ uâ‚‚ := Uniform.continuousWithinAt_iff'_left.1 hFc hâ‚‚
  have C : âˆ€á¶  y in ğ“[s] x, (f y, F x) âˆˆ uâ‚ :=
    (A.and B).mono fun y hy => uâ‚‚â‚ (prod_mk_mem_compRel hy.1 hy.2)
  have : (F x, f x) âˆˆ uâ‚ :=
    uâ‚‚â‚ (prod_mk_mem_compRel (refl_mem_uniformity hâ‚‚) (hsymm (A.self_of_nhdsWithin hx)))
  exact C.mono fun y hy => uâ‚â‚€ (prod_mk_mem_compRel hy this)

/-- A function which can be locally uniformly approximated by functions which are continuous at
a point is continuous at this point. -/
theorem continuousAt_of_locally_uniform_approx_of_continuousAt
    (L : âˆ€ u âˆˆ ğ“¤ Î², âˆƒ t âˆˆ ğ“ x, âˆƒ F, ContinuousAt F x âˆ§ âˆ€ y âˆˆ t, (f y, F y) âˆˆ u) :
    ContinuousAt f x := by
  rw [â† continuousWithinAt_univ]
  apply continuousWithinAt_of_locally_uniform_approx_of_continuousWithinAt (mem_univ _) _
  simpa only [exists_prop, nhdsWithin_univ, continuousWithinAt_univ] using L

/-- A function which can be locally uniformly approximated by functions which are continuous
on a set is continuous on this set. -/
theorem continuousOn_of_locally_uniform_approx_of_continuousWithinAt
    (L : âˆ€ x âˆˆ s, âˆ€ u âˆˆ ğ“¤ Î², âˆƒ t âˆˆ ğ“[s] x, âˆƒ F,
      ContinuousWithinAt F s x âˆ§ âˆ€ y âˆˆ t, (f y, F y) âˆˆ u) :
    ContinuousOn f s := fun x hx =>
  continuousWithinAt_of_locally_uniform_approx_of_continuousWithinAt hx (L x hx)

/-- A function which can be uniformly approximated by functions which are continuous on a set
is continuous on this set. -/
theorem continuousOn_of_uniform_approx_of_continuousOn
    (L : âˆ€ u âˆˆ ğ“¤ Î², âˆƒ F, ContinuousOn F s âˆ§ âˆ€ y âˆˆ s, (f y, F y) âˆˆ u) : ContinuousOn f s :=
  continuousOn_of_locally_uniform_approx_of_continuousWithinAt fun _x hx u hu =>
    âŸ¨s, self_mem_nhdsWithin, (L u hu).imp fun _F hF => âŸ¨hF.1.continuousWithinAt hx, hF.2âŸ©âŸ©

/-- A function which can be locally uniformly approximated by continuous functions is continuous. -/
theorem continuous_of_locally_uniform_approx_of_continuousAt
    (L : âˆ€ x : Î±, âˆ€ u âˆˆ ğ“¤ Î², âˆƒ t âˆˆ ğ“ x, âˆƒ F, ContinuousAt F x âˆ§ âˆ€ y âˆˆ t, (f y, F y) âˆˆ u) :
    Continuous f :=
  continuous_iff_continuousAt.2 fun x =>
    continuousAt_of_locally_uniform_approx_of_continuousAt (L x)

/-- A function which can be uniformly approximated by continuous functions is continuous. -/
theorem continuous_of_uniform_approx_of_continuous
    (L : âˆ€ u âˆˆ ğ“¤ Î², âˆƒ F, Continuous F âˆ§ âˆ€ y, (f y, F y) âˆˆ u) : Continuous f :=
  continuous_iff_continuousOn_univ.mpr <|
    continuousOn_of_uniform_approx_of_continuousOn <| by
      simpa [continuous_iff_continuousOn_univ] using L

/-!
### Uniform limits

From the previous statements on uniform approximation, we deduce continuity results for uniform
limits.
-/


/-- A locally uniform limit on a set of functions which are continuous on this set is itself
continuous on this set. -/
protected theorem TendstoLocallyUniformlyOn.continuousOn (h : TendstoLocallyUniformlyOn F f p s)
    (hc : âˆ€á¶  n in p, ContinuousOn (F n) s) [NeBot p] : ContinuousOn f s := by
  refine continuousOn_of_locally_uniform_approx_of_continuousWithinAt fun x hx u hu => ?_
  rcases h u hu x hx with âŸ¨t, ht, HâŸ©
  rcases (hc.and H).exists with âŸ¨n, hFc, hFâŸ©
  exact âŸ¨t, ht, âŸ¨F n, hFc.continuousWithinAt hx, hFâŸ©âŸ©

/-- A uniform limit on a set of functions which are continuous on this set is itself continuous
on this set. -/
protected theorem TendstoUniformlyOn.continuousOn (h : TendstoUniformlyOn F f p s)
    (hc : âˆ€á¶  n in p, ContinuousOn (F n) s) [NeBot p] : ContinuousOn f s :=
  h.tendstoLocallyUniformlyOn.continuousOn hc

/-- A locally uniform limit of continuous functions is continuous. -/
protected theorem TendstoLocallyUniformly.continuous (h : TendstoLocallyUniformly F f p)
    (hc : âˆ€á¶  n in p, Continuous (F n)) [NeBot p] : Continuous f :=
  continuous_iff_continuousOn_univ.mpr <|
    h.tendstoLocallyUniformlyOn.continuousOn <| hc.mono fun _n hn => hn.continuousOn

/-- A uniform limit of continuous functions is continuous. -/
protected theorem TendstoUniformly.continuous (h : TendstoUniformly F f p)
    (hc : âˆ€á¶  n in p, Continuous (F n)) [NeBot p] : Continuous f :=
  h.tendstoLocallyUniformly.continuous hc

/-!
### Composing limits under uniform convergence

In general, if `Fâ‚™` converges pointwise to a function `f`, and `gâ‚™` tends to `x`, it is not true
that `Fâ‚™ gâ‚™` tends to `f x`. It is true however if the convergence of `Fâ‚™` to `f` is uniform. In
this paragraph, we prove variations around this statement.
-/


/-- If `Fâ‚™` converges locally uniformly on a neighborhood of `x` within a set `s` to a function `f`
which is continuous at `x` within `s`, and `gâ‚™` tends to `x` within `s`, then `Fâ‚™ (gâ‚™)` tends
to `f x`. -/
theorem tendsto_comp_of_locally_uniform_limit_within (h : ContinuousWithinAt f s x)
    (hg : Tendsto g p (ğ“[s] x))
    (hunif : âˆ€ u âˆˆ ğ“¤ Î², âˆƒ t âˆˆ ğ“[s] x, âˆ€á¶  n in p, âˆ€ y âˆˆ t, (f y, F n y) âˆˆ u) :
    Tendsto (fun n => F n (g n)) p (ğ“ (f x)) := by
  refine Uniform.tendsto_nhds_right.2 fun uâ‚€ huâ‚€ => ?_
  obtain âŸ¨uâ‚, hâ‚, uâ‚â‚€âŸ© : âˆƒ u âˆˆ ğ“¤ Î², u â—‹ u âŠ† uâ‚€ := comp_mem_uniformity_sets huâ‚€
  rcases hunif uâ‚ hâ‚ with âŸ¨s, sx, hsâŸ©
  have A : âˆ€á¶  n in p, g n âˆˆ s := hg sx
  have B : âˆ€á¶  n in p, (f x, f (g n)) âˆˆ uâ‚ := hg (Uniform.continuousWithinAt_iff'_right.1 h hâ‚)
  exact B.mp <| A.mp <| hs.mono fun y H1 H2 H3 => uâ‚â‚€ (prod_mk_mem_compRel H3 (H1 _ H2))

/-- If `Fâ‚™` converges locally uniformly on a neighborhood of `x` to a function `f` which is
continuous at `x`, and `gâ‚™` tends to `x`, then `Fâ‚™ (gâ‚™)` tends to `f x`. -/
theorem tendsto_comp_of_locally_uniform_limit (h : ContinuousAt f x) (hg : Tendsto g p (ğ“ x))
    (hunif : âˆ€ u âˆˆ ğ“¤ Î², âˆƒ t âˆˆ ğ“ x, âˆ€á¶  n in p, âˆ€ y âˆˆ t, (f y, F n y) âˆˆ u) :
    Tendsto (fun n => F n (g n)) p (ğ“ (f x)) := by
  rw [â† continuousWithinAt_univ] at h
  rw [â† nhdsWithin_univ] at hunif hg
  exact tendsto_comp_of_locally_uniform_limit_within h hg hunif

/-- If `Fâ‚™` tends locally uniformly to `f` on a set `s`, and `gâ‚™` tends to `x` within `s`, then
`Fâ‚™ gâ‚™` tends to `f x` if `f` is continuous at `x` within `s` and `x âˆˆ s`. -/
theorem TendstoLocallyUniformlyOn.tendsto_comp (h : TendstoLocallyUniformlyOn F f p s)
    (hf : ContinuousWithinAt f s x) (hx : x âˆˆ s) (hg : Tendsto g p (ğ“[s] x)) :
    Tendsto (fun n => F n (g n)) p (ğ“ (f x)) :=
  tendsto_comp_of_locally_uniform_limit_within hf hg fun u hu => h u hu x hx

/-- If `Fâ‚™` tends uniformly to `f` on a set `s`, and `gâ‚™` tends to `x` within `s`, then `Fâ‚™ gâ‚™`
tends to `f x` if `f` is continuous at `x` within `s`. -/
theorem TendstoUniformlyOn.tendsto_comp (h : TendstoUniformlyOn F f p s)
    (hf : ContinuousWithinAt f s x) (hg : Tendsto g p (ğ“[s] x)) :
    Tendsto (fun n => F n (g n)) p (ğ“ (f x)) :=
  tendsto_comp_of_locally_uniform_limit_within hf hg fun u hu => âŸ¨s, self_mem_nhdsWithin, h u huâŸ©

/-- If `Fâ‚™` tends locally uniformly to `f`, and `gâ‚™` tends to `x`, then `Fâ‚™ gâ‚™` tends to `f x`. -/
theorem TendstoLocallyUniformly.tendsto_comp (h : TendstoLocallyUniformly F f p)
    (hf : ContinuousAt f x) (hg : Tendsto g p (ğ“ x)) : Tendsto (fun n => F n (g n)) p (ğ“ (f x)) :=
  tendsto_comp_of_locally_uniform_limit hf hg fun u hu => h u hu x

/-- If `Fâ‚™` tends uniformly to `f`, and `gâ‚™` tends to `x`, then `Fâ‚™ gâ‚™` tends to `f x`. -/
theorem TendstoUniformly.tendsto_comp (h : TendstoUniformly F f p) (hf : ContinuousAt f x)
    (hg : Tendsto g p (ğ“ x)) : Tendsto (fun n => F n (g n)) p (ğ“ (f x)) :=
  h.tendstoLocallyUniformly.tendsto_comp hf hg
