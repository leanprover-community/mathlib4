/-
Copyright (c) 2020 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Topology.UniformSpace.Cauchy

/-!
# Uniform convergence

A sequence of functions `F‚Çô` (with values in a metric space) converges uniformly on a set `s` to a
function `f` if, for all `Œµ > 0`, for all large enough `n`, one has for all `y ‚àà s` the inequality
`dist (f y, F‚Çô y) < Œµ`. Under uniform convergence, many properties of the `F‚Çô` pass to the limit,
most notably continuity. We prove this in the file, defining the notion of uniform convergence
in the more general setting of uniform spaces, and with respect to an arbitrary indexing set
endowed with a filter (instead of just `‚Ñï` with `atTop`).

## Main results

Let `Œ±` be a topological space, `Œ≤` a uniform space, `F‚Çô` and `f` be functions from `Œ±` to `Œ≤`
(where the index `n` belongs to an indexing type `Œπ` endowed with a filter `p`).

* `TendstoUniformlyOn F f p s`: the fact that `F‚Çô` converges uniformly to `f` on `s`. This means
  that, for any entourage `u` of the diagonal, for large enough `n` (with respect to `p`), one has
  `(f y, F‚Çô y) ‚àà u` for all `y ‚àà s`.
* `TendstoUniformly F f p`: same notion with `s = univ`.
* `TendstoUniformlyOn.continuousOn`: a uniform limit on a set of functions which are continuous
  on this set is itself continuous on this set.
* `TendstoUniformly.continuous`: a uniform limit of continuous functions is continuous.
* `TendstoUniformlyOn.tendsto_comp`: If `F‚Çô` tends uniformly to `f` on a set `s`, and `g‚Çô` tends
  to `x` within `s`, then `F‚Çô g‚Çô` tends to `f x` if `f` is continuous at `x` within `s`.
* `TendstoUniformly.tendsto_comp`: If `F‚Çô` tends uniformly to `f`, and `g‚Çô` tends to `x`, then
  `F‚Çô g‚Çô` tends to `f x`.

We also define notions where the convergence is locally uniform, called
`TendstoLocallyUniformlyOn F f p s` and `TendstoLocallyUniformly F f p`. The previous theorems
all have corresponding versions under locally uniform convergence.

Finally, we introduce the notion of a uniform Cauchy sequence, which is to uniform
convergence what a Cauchy sequence is to the usual notion of convergence.

## Implementation notes

We derive most of our initial results from an auxiliary definition `TendstoUniformlyOnFilter`.
This definition in and of itself can sometimes be useful, e.g., when studying the local behavior
of the `F‚Çô` near a point, which would typically look like `TendstoUniformlyOnFilter F f p (ùìù x)`.
Still, while this may be the "correct" definition (see
`tendstoUniformlyOn_iff_tendstoUniformlyOnFilter`), it is somewhat unwieldy to work with in
practice. Thus, we provide the more traditional definition in `TendstoUniformlyOn`.

Most results hold under weaker assumptions of locally uniform approximation. In a first section,
we prove the results under these weaker assumptions. Then, we derive the results on uniform
convergence from them.

## Tags

Uniform limit, uniform convergence, tends uniformly to
 -/


noncomputable section

open Topology Uniformity Filter Set Uniform

universe u v w x
variable {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w} {Œπ : Type x} [UniformSpace Œ≤]
variable {F : Œπ ‚Üí Œ± ‚Üí Œ≤} {f : Œ± ‚Üí Œ≤} {s s' : Set Œ±} {x : Œ±} {p : Filter Œπ} {p' : Filter Œ±}
  {g : Œπ ‚Üí Œ±}

/-!
### Different notions of uniform convergence

We define uniform convergence and locally uniform convergence, on a set or in the whole space.
-/


/-- A sequence of functions `F‚Çô` converges uniformly on a filter `p'` to a limiting function `f`
with respect to the filter `p` if, for any entourage of the diagonal `u`, one has
`p √óÀ¢ p'`-eventually `(f x, F‚Çô x) ‚àà u`. -/
def TendstoUniformlyOnFilter (F : Œπ ‚Üí Œ± ‚Üí Œ≤) (f : Œ± ‚Üí Œ≤) (p : Filter Œπ) (p' : Filter Œ±) :=
  ‚àÄ u ‚àà ùì§ Œ≤, ‚àÄ·∂† n : Œπ √ó Œ± in p √óÀ¢ p', (f n.snd, F n.fst n.snd) ‚àà u

/--
A sequence of functions `F‚Çô` converges uniformly on a filter `p'` to a limiting function `f` w.r.t.
filter `p` iff the function `(n, x) ‚Ü¶ (f x, F‚Çô x)` converges along `p √óÀ¢ p'` to the uniformity.
In other words: one knows nothing about the behavior of `x` in this limit besides it being in `p'`.
-/
theorem tendstoUniformlyOnFilter_iff_tendsto :
    TendstoUniformlyOnFilter F f p p' ‚Üî
      Tendsto (fun q : Œπ √ó Œ± => (f q.2, F q.1 q.2)) (p √óÀ¢ p') (ùì§ Œ≤) :=
  Iff.rfl

/-- A sequence of functions `F‚Çô` converges uniformly on a set `s` to a limiting function `f` with
respect to the filter `p` if, for any entourage of the diagonal `u`, one has `p`-eventually
`(f x, F‚Çô x) ‚àà u` for all `x ‚àà s`. -/
def TendstoUniformlyOn (F : Œπ ‚Üí Œ± ‚Üí Œ≤) (f : Œ± ‚Üí Œ≤) (p : Filter Œπ) (s : Set Œ±) :=
  ‚àÄ u ‚àà ùì§ Œ≤, ‚àÄ·∂† n in p, ‚àÄ x : Œ±, x ‚àà s ‚Üí (f x, F n x) ‚àà u

theorem tendstoUniformlyOn_iff_tendstoUniformlyOnFilter :
    TendstoUniformlyOn F f p s ‚Üî TendstoUniformlyOnFilter F f p (ùìü s) := by
  simp only [TendstoUniformlyOn, TendstoUniformlyOnFilter]
  apply forall‚ÇÇ_congr
  simp_rw [eventually_prod_principal_iff]
  simp


alias ‚ü®TendstoUniformlyOn.tendstoUniformlyOnFilter, TendstoUniformlyOnFilter.tendstoUniformlyOn‚ü© :=
  tendstoUniformlyOn_iff_tendstoUniformlyOnFilter

/-- A sequence of functions `F‚Çô` converges uniformly on a set `s` to a limiting function `f` w.r.t.
filter `p` iff the function `(n, x) ‚Ü¶ (f x, F‚Çô x)` converges along `p √óÀ¢ ùìü s` to the uniformity.
In other words: one knows nothing about the behavior of `x` in this limit besides it being in `s`.
-/
theorem tendstoUniformlyOn_iff_tendsto {F : Œπ ‚Üí Œ± ‚Üí Œ≤} {f : Œ± ‚Üí Œ≤} {p : Filter Œπ} {s : Set Œ±} :
    TendstoUniformlyOn F f p s ‚Üî
    Tendsto (fun q : Œπ √ó Œ± => (f q.2, F q.1 q.2)) (p √óÀ¢ ùìü s) (ùì§ Œ≤) := by
  simp [tendstoUniformlyOn_iff_tendstoUniformlyOnFilter, tendstoUniformlyOnFilter_iff_tendsto]

/-- A sequence of functions `F‚Çô` converges uniformly to a limiting function `f` with respect to a
filter `p` if, for any entourage of the diagonal `u`, one has `p`-eventually
`(f x, F‚Çô x) ‚àà u` for all `x`. -/
def TendstoUniformly (F : Œπ ‚Üí Œ± ‚Üí Œ≤) (f : Œ± ‚Üí Œ≤) (p : Filter Œπ) :=
  ‚àÄ u ‚àà ùì§ Œ≤, ‚àÄ·∂† n in p, ‚àÄ x : Œ±, (f x, F n x) ‚àà u

-- Porting note: moved from below
theorem tendstoUniformlyOn_univ : TendstoUniformlyOn F f p univ ‚Üî TendstoUniformly F f p := by
  simp [TendstoUniformlyOn, TendstoUniformly]

theorem tendstoUniformly_iff_tendstoUniformlyOnFilter :
    TendstoUniformly F f p ‚Üî TendstoUniformlyOnFilter F f p ‚ä§ := by
  rw [‚Üê tendstoUniformlyOn_univ, tendstoUniformlyOn_iff_tendstoUniformlyOnFilter, principal_univ]

theorem TendstoUniformly.tendstoUniformlyOnFilter (h : TendstoUniformly F f p) :
    TendstoUniformlyOnFilter F f p ‚ä§ := by rwa [‚Üê tendstoUniformly_iff_tendstoUniformlyOnFilter]

theorem tendstoUniformlyOn_iff_tendstoUniformly_comp_coe :
    TendstoUniformlyOn F f p s ‚Üî TendstoUniformly (fun i (x : s) => F i x) (f ‚àò (‚Üë)) p :=
  forall‚ÇÇ_congr fun u _ => by simp

/-- A sequence of functions `F‚Çô` converges uniformly to a limiting function `f` w.r.t.
filter `p` iff the function `(n, x) ‚Ü¶ (f x, F‚Çô x)` converges along `p √óÀ¢ ‚ä§` to the uniformity.
In other words: one knows nothing about the behavior of `x` in this limit.
-/
theorem tendstoUniformly_iff_tendsto {F : Œπ ‚Üí Œ± ‚Üí Œ≤} {f : Œ± ‚Üí Œ≤} {p : Filter Œπ} :
    TendstoUniformly F f p ‚Üî Tendsto (fun q : Œπ √ó Œ± => (f q.2, F q.1 q.2)) (p √óÀ¢ ‚ä§) (ùì§ Œ≤) := by
  simp [tendstoUniformly_iff_tendstoUniformlyOnFilter, tendstoUniformlyOnFilter_iff_tendsto]

/-- Uniform converence implies pointwise convergence. -/
theorem TendstoUniformlyOnFilter.tendsto_at (h : TendstoUniformlyOnFilter F f p p')
    (hx : ùìü {x} ‚â§ p') : Tendsto (fun n => F n x) p <| ùìù (f x) := by
  refine Uniform.tendsto_nhds_right.mpr fun u hu => mem_map.mpr ?_
  filter_upwards [(h u hu).curry]
  intro i h
  simpa using h.filter_mono hx

/-- Uniform converence implies pointwise convergence. -/
theorem TendstoUniformlyOn.tendsto_at (h : TendstoUniformlyOn F f p s) {x : Œ±} (hx : x ‚àà s) :
    Tendsto (fun n => F n x) p <| ùìù (f x) :=
  h.tendstoUniformlyOnFilter.tendsto_at
    (le_principal_iff.mpr <| mem_principal.mpr <| singleton_subset_iff.mpr <| hx)

/-- Uniform converence implies pointwise convergence. -/
theorem TendstoUniformly.tendsto_at (h : TendstoUniformly F f p) (x : Œ±) :
    Tendsto (fun n => F n x) p <| ùìù (f x) :=
  h.tendstoUniformlyOnFilter.tendsto_at le_top

-- Porting note: tendstoUniformlyOn_univ moved up

theorem TendstoUniformlyOnFilter.mono_left {p'' : Filter Œπ} (h : TendstoUniformlyOnFilter F f p p')
    (hp : p'' ‚â§ p) : TendstoUniformlyOnFilter F f p'' p' := fun u hu =>
  (h u hu).filter_mono (p'.prod_mono_left hp)

theorem TendstoUniformlyOnFilter.mono_right {p'' : Filter Œ±} (h : TendstoUniformlyOnFilter F f p p')
    (hp : p'' ‚â§ p') : TendstoUniformlyOnFilter F f p p'' := fun u hu =>
  (h u hu).filter_mono (p.prod_mono_right hp)

theorem TendstoUniformlyOn.mono {s' : Set Œ±} (h : TendstoUniformlyOn F f p s) (h' : s' ‚äÜ s) :
    TendstoUniformlyOn F f p s' :=
  tendstoUniformlyOn_iff_tendstoUniformlyOnFilter.mpr
    (h.tendstoUniformlyOnFilter.mono_right (le_principal_iff.mpr <| mem_principal.mpr h'))

theorem TendstoUniformlyOnFilter.congr {F' : Œπ ‚Üí Œ± ‚Üí Œ≤} (hf : TendstoUniformlyOnFilter F f p p')
    (hff' : ‚àÄ·∂† n : Œπ √ó Œ± in p √óÀ¢ p', F n.fst n.snd = F' n.fst n.snd) :
    TendstoUniformlyOnFilter F' f p p' := by
  refine fun u hu => ((hf u hu).and hff').mono fun n h => ?_
  rw [‚Üê h.right]
  exact h.left

theorem TendstoUniformlyOn.congr {F' : Œπ ‚Üí Œ± ‚Üí Œ≤} (hf : TendstoUniformlyOn F f p s)
    (hff' : ‚àÄ·∂† n in p, Set.EqOn (F n) (F' n) s) : TendstoUniformlyOn F' f p s := by
  rw [tendstoUniformlyOn_iff_tendstoUniformlyOnFilter] at hf ‚ä¢
  refine hf.congr ?_
  rw [eventually_iff] at hff' ‚ä¢
  simp only [Set.EqOn] at hff'
  simp only [mem_prod_principal, hff', mem_setOf_eq]

lemma tendstoUniformly_congr {F F' : Œπ ‚Üí Œ± ‚Üí Œ≤} {f : Œ± ‚Üí Œ≤} (hF : F =·∂†[p] F') :
    TendstoUniformly F f p ‚Üî TendstoUniformly F' f p := by
  simp_rw [‚Üê tendstoUniformlyOn_univ] at *
  have HF := EventuallyEq.exists_mem hF
  exact ‚ü®fun h => h.congr (by aesop), fun h => h.congr (by simp_rw [eqOn_comm]; aesop)‚ü©

theorem TendstoUniformlyOn.congr_right {g : Œ± ‚Üí Œ≤} (hf : TendstoUniformlyOn F f p s)
    (hfg : EqOn f g s) : TendstoUniformlyOn F g p s := fun u hu => by
  filter_upwards [hf u hu] with i hi a ha using hfg ha ‚ñ∏ hi a ha

protected theorem TendstoUniformly.tendstoUniformlyOn (h : TendstoUniformly F f p) :
    TendstoUniformlyOn F f p s :=
  (tendstoUniformlyOn_univ.2 h).mono (subset_univ s)

/-- Composing on the right by a function preserves uniform convergence on a filter -/
theorem TendstoUniformlyOnFilter.comp (h : TendstoUniformlyOnFilter F f p p') (g : Œ≥ ‚Üí Œ±) :
    TendstoUniformlyOnFilter (fun n => F n ‚àò g) (f ‚àò g) p (p'.comap g) := by
  rw [tendstoUniformlyOnFilter_iff_tendsto] at h ‚ä¢
  exact h.comp (tendsto_id.prod_map tendsto_comap)

/-- Composing on the right by a function preserves uniform convergence on a set -/
theorem TendstoUniformlyOn.comp (h : TendstoUniformlyOn F f p s) (g : Œ≥ ‚Üí Œ±) :
    TendstoUniformlyOn (fun n => F n ‚àò g) (f ‚àò g) p (g ‚Åª¬π' s) := by
  rw [tendstoUniformlyOn_iff_tendstoUniformlyOnFilter] at h ‚ä¢
  simpa [TendstoUniformlyOn, comap_principal] using TendstoUniformlyOnFilter.comp h g

/-- Composing on the right by a function preserves uniform convergence -/
theorem TendstoUniformly.comp (h : TendstoUniformly F f p) (g : Œ≥ ‚Üí Œ±) :
    TendstoUniformly (fun n => F n ‚àò g) (f ‚àò g) p := by
  rw [tendstoUniformly_iff_tendstoUniformlyOnFilter] at h ‚ä¢
  simpa [principal_univ, comap_principal] using h.comp g

/-- Composing on the left by a uniformly continuous function preserves
  uniform convergence on a filter -/
theorem UniformContinuous.comp_tendstoUniformlyOnFilter [UniformSpace Œ≥] {g : Œ≤ ‚Üí Œ≥}
    (hg : UniformContinuous g) (h : TendstoUniformlyOnFilter F f p p') :
    TendstoUniformlyOnFilter (fun i => g ‚àò F i) (g ‚àò f) p p' := fun _u hu => h _ (hg hu)

/-- Composing on the left by a uniformly continuous function preserves
  uniform convergence on a set -/
theorem UniformContinuous.comp_tendstoUniformlyOn [UniformSpace Œ≥] {g : Œ≤ ‚Üí Œ≥}
    (hg : UniformContinuous g) (h : TendstoUniformlyOn F f p s) :
    TendstoUniformlyOn (fun i => g ‚àò F i) (g ‚àò f) p s := fun _u hu => h _ (hg hu)

/-- Composing on the left by a uniformly continuous function preserves uniform convergence -/
theorem UniformContinuous.comp_tendstoUniformly [UniformSpace Œ≥] {g : Œ≤ ‚Üí Œ≥}
    (hg : UniformContinuous g) (h : TendstoUniformly F f p) :
    TendstoUniformly (fun i => g ‚àò F i) (g ‚àò f) p := fun _u hu => h _ (hg hu)

theorem TendstoUniformlyOnFilter.prod_map {Œπ' Œ±' Œ≤' : Type*} [UniformSpace Œ≤'] {F' : Œπ' ‚Üí Œ±' ‚Üí Œ≤'}
    {f' : Œ±' ‚Üí Œ≤'} {q : Filter Œπ'} {q' : Filter Œ±'} (h : TendstoUniformlyOnFilter F f p p')
    (h' : TendstoUniformlyOnFilter F' f' q q') :
    TendstoUniformlyOnFilter (fun i : Œπ √ó Œπ' => Prod.map (F i.1) (F' i.2)) (Prod.map f f')
      (p √óÀ¢ q) (p' √óÀ¢ q') := by
  rw [tendstoUniformlyOnFilter_iff_tendsto] at h h' ‚ä¢
  rw [uniformity_prod_eq_comap_prod, tendsto_comap_iff, ‚Üê map_swap4_prod, tendsto_map'_iff]
  convert h.prod_map h' -- seems to be faster than `exact` here

theorem TendstoUniformlyOn.prod_map {Œπ' Œ±' Œ≤' : Type*} [UniformSpace Œ≤'] {F' : Œπ' ‚Üí Œ±' ‚Üí Œ≤'}
    {f' : Œ±' ‚Üí Œ≤'} {p' : Filter Œπ'} {s' : Set Œ±'} (h : TendstoUniformlyOn F f p s)
    (h' : TendstoUniformlyOn F' f' p' s') :
    TendstoUniformlyOn (fun i : Œπ √ó Œπ' => Prod.map (F i.1) (F' i.2)) (Prod.map f f') (p √óÀ¢ p')
      (s √óÀ¢ s') := by
  rw [tendstoUniformlyOn_iff_tendstoUniformlyOnFilter] at h h' ‚ä¢
  simpa only [prod_principal_principal] using h.prod_map h'

theorem TendstoUniformly.prod_map {Œπ' Œ±' Œ≤' : Type*} [UniformSpace Œ≤'] {F' : Œπ' ‚Üí Œ±' ‚Üí Œ≤'}
    {f' : Œ±' ‚Üí Œ≤'} {p' : Filter Œπ'} (h : TendstoUniformly F f p) (h' : TendstoUniformly F' f' p') :
    TendstoUniformly (fun i : Œπ √ó Œπ' => Prod.map (F i.1) (F' i.2)) (Prod.map f f') (p √óÀ¢ p') := by
  rw [‚Üê tendstoUniformlyOn_univ, ‚Üê univ_prod_univ] at *
  exact h.prod_map h'

theorem TendstoUniformlyOnFilter.prod {Œπ' Œ≤' : Type*} [UniformSpace Œ≤'] {F' : Œπ' ‚Üí Œ± ‚Üí Œ≤'}
    {f' : Œ± ‚Üí Œ≤'} {q : Filter Œπ'} (h : TendstoUniformlyOnFilter F f p p')
    (h' : TendstoUniformlyOnFilter F' f' q p') :
    TendstoUniformlyOnFilter (fun (i : Œπ √ó Œπ') a => (F i.1 a, F' i.2 a)) (fun a => (f a, f' a))
      (p √óÀ¢ q) p' :=
  fun u hu => ((h.prod_map h') u hu).diag_of_prod_right

protected theorem TendstoUniformlyOn.prod {Œπ' Œ≤' : Type*} [UniformSpace Œ≤']
    {F' : Œπ' ‚Üí Œ± ‚Üí Œ≤'} {f' : Œ± ‚Üí Œ≤'} {p' : Filter Œπ'}
    (h : TendstoUniformlyOn F f p s) (h' : TendstoUniformlyOn F' f' p' s) :
    TendstoUniformlyOn (fun (i : Œπ √ó Œπ') a => (F i.1 a, F' i.2 a)) (fun a => (f a, f' a))
      (p √óÀ¢ p') s :=
  (congr_arg _ s.inter_self).mp ((h.prod_map h').comp fun a => (a, a))

theorem TendstoUniformly.prod {Œπ' Œ≤' : Type*} [UniformSpace Œ≤'] {F' : Œπ' ‚Üí Œ± ‚Üí Œ≤'} {f' : Œ± ‚Üí Œ≤'}
    {p' : Filter Œπ'} (h : TendstoUniformly F f p) (h' : TendstoUniformly F' f' p') :
    TendstoUniformly (fun (i : Œπ √ó Œπ') a => (F i.1 a, F' i.2 a)) (fun a => (f a, f' a))
      (p √óÀ¢ p') :=
  (h.prod_map h').comp fun a => (a, a)

/-- Uniform convergence on a filter `p'` to a constant function is equivalent to convergence in
`p √óÀ¢ p'`. -/
theorem tendsto_prod_filter_iff {c : Œ≤} :
    Tendsto (‚ÜøF) (p √óÀ¢ p') (ùìù c) ‚Üî TendstoUniformlyOnFilter F (fun _ => c) p p' := by
  simp_rw [nhds_eq_comap_uniformity, tendsto_comap_iff]
  rfl

/-- Uniform convergence on a set `s` to a constant function is equivalent to convergence in
`p √óÀ¢ ùìü s`. -/
theorem tendsto_prod_principal_iff {c : Œ≤} :
    Tendsto (‚ÜøF) (p √óÀ¢ ùìü s) (ùìù c) ‚Üî TendstoUniformlyOn F (fun _ => c) p s := by
  rw [tendstoUniformlyOn_iff_tendstoUniformlyOnFilter]
  exact tendsto_prod_filter_iff

/-- Uniform convergence to a constant function is equivalent to convergence in `p √óÀ¢ ‚ä§`. -/
theorem tendsto_prod_top_iff {c : Œ≤} :
    Tendsto (‚ÜøF) (p √óÀ¢ ‚ä§) (ùìù c) ‚Üî TendstoUniformly F (fun _ => c) p := by
  rw [tendstoUniformly_iff_tendstoUniformlyOnFilter]
  exact tendsto_prod_filter_iff

/-- Uniform convergence on the empty set is vacuously true -/
theorem tendstoUniformlyOn_empty : TendstoUniformlyOn F f p ‚àÖ := fun u _ => by simp

/-- Uniform convergence on a singleton is equivalent to regular convergence -/
theorem tendstoUniformlyOn_singleton_iff_tendsto :
    TendstoUniformlyOn F f p {x} ‚Üî Tendsto (fun n : Œπ => F n x) p (ùìù (f x)) := by
  simp_rw [tendstoUniformlyOn_iff_tendsto, Uniform.tendsto_nhds_right, tendsto_def]
  exact forall‚ÇÇ_congr fun u _ => by simp [mem_prod_principal, preimage]

/-- If a sequence `g` converges to some `b`, then the sequence of constant functions
`fun n ‚Ü¶ fun a ‚Ü¶ g n` converges to the constant function `fun a ‚Ü¶ b` on any set `s` -/
theorem Filter.Tendsto.tendstoUniformlyOnFilter_const {g : Œπ ‚Üí Œ≤} {b : Œ≤} (hg : Tendsto g p (ùìù b))
    (p' : Filter Œ±) :
    TendstoUniformlyOnFilter (fun n : Œπ => fun _ : Œ± => g n) (fun _ : Œ± => b) p p' := by
  simpa only [nhds_eq_comap_uniformity, tendsto_comap_iff] using hg.comp (tendsto_fst (g := p'))

/-- If a sequence `g` converges to some `b`, then the sequence of constant functions
`fun n ‚Ü¶ fun a ‚Ü¶ g n` converges to the constant function `fun a ‚Ü¶ b` on any set `s` -/
theorem Filter.Tendsto.tendstoUniformlyOn_const {g : Œπ ‚Üí Œ≤} {b : Œ≤} (hg : Tendsto g p (ùìù b))
    (s : Set Œ±) : TendstoUniformlyOn (fun n : Œπ => fun _ : Œ± => g n) (fun _ : Œ± => b) p s :=
  tendstoUniformlyOn_iff_tendstoUniformlyOnFilter.mpr (hg.tendstoUniformlyOnFilter_const (ùìü s))

theorem UniformContinuousOn.tendstoUniformlyOn [UniformSpace Œ±] [UniformSpace Œ≥] {x : Œ±} {U : Set Œ±}
    {V : Set Œ≤} {F : Œ± ‚Üí Œ≤ ‚Üí Œ≥} (hF : UniformContinuousOn (‚ÜøF) (U √óÀ¢ V)) (hU : x ‚àà U) :
    TendstoUniformlyOn F (F x) (ùìù[U] x) V := by
  set œÜ := fun q : Œ± √ó Œ≤ => ((x, q.2), q)
  rw [tendstoUniformlyOn_iff_tendsto]
  change Tendsto (Prod.map (‚ÜøF) ‚ÜøF ‚àò œÜ) (ùìù[U] x √óÀ¢ ùìü V) (ùì§ Œ≥)
  simp only [nhdsWithin, Filter.prod_eq_inf, comap_inf, inf_assoc, comap_principal, inf_principal]
  refine hF.comp (Tendsto.inf ?_ <| tendsto_principal_principal.2 fun x hx => ‚ü®‚ü®hU, hx.2‚ü©, hx‚ü©)
  simp only [uniformity_prod_eq_comap_prod, tendsto_comap_iff, (¬∑ ‚àò ¬∑),
    nhds_eq_comap_uniformity, comap_comap]
  exact tendsto_comap.prod_mk (tendsto_diag_uniformity _ _)

theorem UniformContinuousOn.tendstoUniformly [UniformSpace Œ±] [UniformSpace Œ≥] {x : Œ±} {U : Set Œ±}
    (hU : U ‚àà ùìù x) {F : Œ± ‚Üí Œ≤ ‚Üí Œ≥} (hF : UniformContinuousOn (‚ÜøF) (U √óÀ¢ (univ : Set Œ≤))) :
    TendstoUniformly F (F x) (ùìù x) := by
  simpa only [tendstoUniformlyOn_univ, nhdsWithin_eq_nhds.2 hU]
    using hF.tendstoUniformlyOn (mem_of_mem_nhds hU)

theorem UniformContinuous‚ÇÇ.tendstoUniformly [UniformSpace Œ±] [UniformSpace Œ≥] {f : Œ± ‚Üí Œ≤ ‚Üí Œ≥}
    (h : UniformContinuous‚ÇÇ f) {x : Œ±} : TendstoUniformly f (f x) (ùìù x) :=
  UniformContinuousOn.tendstoUniformly univ_mem <| by rwa [univ_prod_univ, uniformContinuousOn_univ]

/-- A sequence is uniformly Cauchy if eventually all of its pairwise differences are
uniformly bounded -/
def UniformCauchySeqOnFilter (F : Œπ ‚Üí Œ± ‚Üí Œ≤) (p : Filter Œπ) (p' : Filter Œ±) : Prop :=
  ‚àÄ u ‚àà ùì§ Œ≤, ‚àÄ·∂† m : (Œπ √ó Œπ) √ó Œ± in (p √óÀ¢ p) √óÀ¢ p', (F m.fst.fst m.snd, F m.fst.snd m.snd) ‚àà u

/-- A sequence is uniformly Cauchy if eventually all of its pairwise differences are
uniformly bounded -/
def UniformCauchySeqOn (F : Œπ ‚Üí Œ± ‚Üí Œ≤) (p : Filter Œπ) (s : Set Œ±) : Prop :=
  ‚àÄ u ‚àà ùì§ Œ≤, ‚àÄ·∂† m : Œπ √ó Œπ in p √óÀ¢ p, ‚àÄ x : Œ±, x ‚àà s ‚Üí (F m.fst x, F m.snd x) ‚àà u

theorem uniformCauchySeqOn_iff_uniformCauchySeqOnFilter :
    UniformCauchySeqOn F p s ‚Üî UniformCauchySeqOnFilter F p (ùìü s) := by
  simp only [UniformCauchySeqOn, UniformCauchySeqOnFilter]
  refine forall‚ÇÇ_congr fun u hu => ?_
  rw [eventually_prod_principal_iff]

theorem UniformCauchySeqOn.uniformCauchySeqOnFilter (hF : UniformCauchySeqOn F p s) :
    UniformCauchySeqOnFilter F p (ùìü s) := by rwa [‚Üê uniformCauchySeqOn_iff_uniformCauchySeqOnFilter]

/-- A sequence that converges uniformly is also uniformly Cauchy -/
theorem TendstoUniformlyOnFilter.uniformCauchySeqOnFilter (hF : TendstoUniformlyOnFilter F f p p') :
    UniformCauchySeqOnFilter F p p' := by
  intro u hu
  rcases comp_symm_of_uniformity hu with ‚ü®t, ht, htsymm, htmem‚ü©
  have := tendsto_swap4_prod.eventually ((hF t ht).prod_mk (hF t ht))
  apply this.diag_of_prod_right.mono
  simp only [and_imp, Prod.forall]
  intro n1 n2 x hl hr
  exact Set.mem_of_mem_of_subset (prod_mk_mem_compRel (htsymm hl) hr) htmem

/-- A sequence that converges uniformly is also uniformly Cauchy -/
theorem TendstoUniformlyOn.uniformCauchySeqOn (hF : TendstoUniformlyOn F f p s) :
    UniformCauchySeqOn F p s :=
  uniformCauchySeqOn_iff_uniformCauchySeqOnFilter.mpr
    hF.tendstoUniformlyOnFilter.uniformCauchySeqOnFilter

/-- A uniformly Cauchy sequence converges uniformly to its limit -/
theorem UniformCauchySeqOnFilter.tendstoUniformlyOnFilter_of_tendsto
    (hF : UniformCauchySeqOnFilter F p p')
    (hF' : ‚àÄ·∂† x : Œ± in p', Tendsto (fun n => F n x) p (ùìù (f x))) :
    TendstoUniformlyOnFilter F f p p' := by
  rcases p.eq_or_neBot with rfl | _
  ¬∑ simp only [TendstoUniformlyOnFilter, bot_prod, eventually_bot, implies_true]
  -- Proof idea: |f_n(x) - f(x)| ‚â§ |f_n(x) - f_m(x)| + |f_m(x) - f(x)|. We choose `n`
  -- so that |f_n(x) - f_m(x)| is uniformly small across `s` whenever `m ‚â• n`. Then for
  -- a fixed `x`, we choose `m` sufficiently large such that |f_m(x) - f(x)| is small.
  intro u hu
  rcases comp_symm_of_uniformity hu with ‚ü®t, ht, htsymm, htmem‚ü©
  -- We will choose n, x, and m simultaneously. n and x come from hF. m comes from hF'
  -- But we need to promote hF' to the full product filter to use it
  have hmc : ‚àÄ·∂† x in (p √óÀ¢ p) √óÀ¢ p', Tendsto (fun n : Œπ => F n x.snd) p (ùìù (f x.snd)) := by
    rw [eventually_prod_iff]
    exact ‚ü®fun _ => True, by simp, _, hF', by simp‚ü©
  -- To apply filter operations we'll need to do some order manipulation
  rw [Filter.eventually_swap_iff]
  have := tendsto_prodAssoc.eventually (tendsto_prod_swap.eventually ((hF t ht).and hmc))
  apply this.curry.mono
  simp only [Equiv.prodAssoc_apply, eventually_and, eventually_const, Prod.snd_swap, Prod.fst_swap,
    and_imp, Prod.forall]
  -- Complete the proof
  intro x n hx hm'
  refine Set.mem_of_mem_of_subset (mem_compRel.mpr ?_) htmem
  rw [Uniform.tendsto_nhds_right] at hm'
  have := hx.and (hm' ht)
  obtain ‚ü®m, hm‚ü© := this.exists
  exact ‚ü®F m x, ‚ü®hm.2, htsymm hm.1‚ü©‚ü©

/-- A uniformly Cauchy sequence converges uniformly to its limit -/
theorem UniformCauchySeqOn.tendstoUniformlyOn_of_tendsto (hF : UniformCauchySeqOn F p s)
    (hF' : ‚àÄ x : Œ±, x ‚àà s ‚Üí Tendsto (fun n => F n x) p (ùìù (f x))) : TendstoUniformlyOn F f p s :=
  tendstoUniformlyOn_iff_tendstoUniformlyOnFilter.mpr
    (hF.uniformCauchySeqOnFilter.tendstoUniformlyOnFilter_of_tendsto hF')

theorem UniformCauchySeqOnFilter.mono_left {p'' : Filter Œπ} (hf : UniformCauchySeqOnFilter F p p')
    (hp : p'' ‚â§ p) : UniformCauchySeqOnFilter F p'' p' := by
  intro u hu
  have := (hf u hu).filter_mono (p'.prod_mono_left (Filter.prod_mono hp hp))
  exact this.mono (by simp)

theorem UniformCauchySeqOnFilter.mono_right {p'' : Filter Œ±} (hf : UniformCauchySeqOnFilter F p p')
    (hp : p'' ‚â§ p') : UniformCauchySeqOnFilter F p p'' := fun u hu =>
  have := (hf u hu).filter_mono ((p √óÀ¢ p).prod_mono_right hp)
  this.mono (by simp)

theorem UniformCauchySeqOn.mono {s' : Set Œ±} (hf : UniformCauchySeqOn F p s) (hss' : s' ‚äÜ s) :
    UniformCauchySeqOn F p s' := by
  rw [uniformCauchySeqOn_iff_uniformCauchySeqOnFilter] at hf ‚ä¢
  exact hf.mono_right (le_principal_iff.mpr <| mem_principal.mpr hss')

/-- Composing on the right by a function preserves uniform Cauchy sequences -/
theorem UniformCauchySeqOnFilter.comp {Œ≥ : Type*} (hf : UniformCauchySeqOnFilter F p p')
    (g : Œ≥ ‚Üí Œ±) : UniformCauchySeqOnFilter (fun n => F n ‚àò g) p (p'.comap g) := fun u hu => by
  obtain ‚ü®pa, hpa, pb, hpb, hpapb‚ü© := eventually_prod_iff.mp (hf u hu)
  rw [eventually_prod_iff]
  refine ‚ü®pa, hpa, pb ‚àò g, ?_, fun hx _ hy => hpapb hx hy‚ü©
  exact eventually_comap.mpr (hpb.mono fun x hx y hy => by simp only [hx, hy, Function.comp_apply])

/-- Composing on the right by a function preserves uniform Cauchy sequences -/
theorem UniformCauchySeqOn.comp {Œ≥ : Type*} (hf : UniformCauchySeqOn F p s) (g : Œ≥ ‚Üí Œ±) :
    UniformCauchySeqOn (fun n => F n ‚àò g) p (g ‚Åª¬π' s) := by
  rw [uniformCauchySeqOn_iff_uniformCauchySeqOnFilter] at hf ‚ä¢
  simpa only [UniformCauchySeqOn, comap_principal] using hf.comp g

/-- Composing on the left by a uniformly continuous function preserves
uniform Cauchy sequences -/
theorem UniformContinuous.comp_uniformCauchySeqOn [UniformSpace Œ≥] {g : Œ≤ ‚Üí Œ≥}
    (hg : UniformContinuous g) (hf : UniformCauchySeqOn F p s) :
    UniformCauchySeqOn (fun n => g ‚àò F n) p s := fun _u hu => hf _ (hg hu)

theorem UniformCauchySeqOn.prod_map {Œπ' Œ±' Œ≤' : Type*} [UniformSpace Œ≤'] {F' : Œπ' ‚Üí Œ±' ‚Üí Œ≤'}
    {p' : Filter Œπ'} {s' : Set Œ±'} (h : UniformCauchySeqOn F p s)
    (h' : UniformCauchySeqOn F' p' s') :
    UniformCauchySeqOn (fun i : Œπ √ó Œπ' => Prod.map (F i.1) (F' i.2)) (p √óÀ¢ p') (s √óÀ¢ s') := by
  intro u hu
  rw [uniformity_prod_eq_prod, mem_map, mem_prod_iff] at hu
  obtain ‚ü®v, hv, w, hw, hvw‚ü© := hu
  simp_rw [mem_prod, and_imp, Prod.forall, Prod.map_apply]
  rw [‚Üê Set.image_subset_iff] at hvw
  apply (tendsto_swap4_prod.eventually ((h v hv).prod_mk (h' w hw))).mono
  intro x hx a b ha hb
  exact hvw ‚ü®_, mk_mem_prod (hx.1 a ha) (hx.2 b hb), rfl‚ü©

theorem UniformCauchySeqOn.prod {Œπ' Œ≤' : Type*} [UniformSpace Œ≤'] {F' : Œπ' ‚Üí Œ± ‚Üí Œ≤'}
    {p' : Filter Œπ'} (h : UniformCauchySeqOn F p s) (h' : UniformCauchySeqOn F' p' s) :
    UniformCauchySeqOn (fun (i : Œπ √ó Œπ') a => (F i.fst a, F' i.snd a)) (p √óÀ¢ p') s :=
  (congr_arg _ s.inter_self).mp ((h.prod_map h').comp fun a => (a, a))

theorem UniformCauchySeqOn.prod' {Œ≤' : Type*} [UniformSpace Œ≤'] {F' : Œπ ‚Üí Œ± ‚Üí Œ≤'}
    (h : UniformCauchySeqOn F p s) (h' : UniformCauchySeqOn F' p s) :
    UniformCauchySeqOn (fun (i : Œπ) a => (F i a, F' i a)) p s := fun u hu =>
  have hh : Tendsto (fun x : Œπ => (x, x)) p (p √óÀ¢ p) := tendsto_diag
  (hh.prod_map hh).eventually ((h.prod h') u hu)

/-- If a sequence of functions is uniformly Cauchy on a set, then the values at each point form
a Cauchy sequence. -/
theorem UniformCauchySeqOn.cauchy_map [hp : NeBot p] (hf : UniformCauchySeqOn F p s) (hx : x ‚àà s) :
    Cauchy (map (fun i => F i x) p) := by
  simp only [cauchy_map_iff, hp, true_and]
  intro u hu
  rw [mem_map]
  filter_upwards [hf u hu] with p hp using hp x hx

/-- If a sequence of functions is uniformly Cauchy on a set, then the values at each point form
a Cauchy sequence.  See `UniformCauchSeqOn.cauchy_map` for the non-`atTop` case. -/
theorem UniformCauchySeqOn.cauchySeq [Nonempty Œπ] [SemilatticeSup Œπ]
    (hf : UniformCauchySeqOn F atTop s) (hx : x ‚àà s) :
    CauchySeq fun i ‚Ü¶ F i x :=
  hf.cauchy_map (hp := atTop_neBot) hx

section SeqTendsto

theorem tendstoUniformlyOn_of_seq_tendstoUniformlyOn {l : Filter Œπ} [l.IsCountablyGenerated]
    (h : ‚àÄ u : ‚Ñï ‚Üí Œπ, Tendsto u atTop l ‚Üí TendstoUniformlyOn (fun n => F (u n)) f atTop s) :
    TendstoUniformlyOn F f l s := by
  rw [tendstoUniformlyOn_iff_tendsto, tendsto_iff_seq_tendsto]
  intro u hu
  rw [tendsto_prod_iff'] at hu
  specialize h (fun n => (u n).fst) hu.1
  rw [tendstoUniformlyOn_iff_tendsto] at h
  exact h.comp (tendsto_id.prod_mk hu.2)

theorem TendstoUniformlyOn.seq_tendstoUniformlyOn {l : Filter Œπ} (h : TendstoUniformlyOn F f l s)
    (u : ‚Ñï ‚Üí Œπ) (hu : Tendsto u atTop l) : TendstoUniformlyOn (fun n => F (u n)) f atTop s := by
  rw [tendstoUniformlyOn_iff_tendsto] at h ‚ä¢
  exact h.comp ((hu.comp tendsto_fst).prod_mk tendsto_snd)

theorem tendstoUniformlyOn_iff_seq_tendstoUniformlyOn {l : Filter Œπ} [l.IsCountablyGenerated] :
    TendstoUniformlyOn F f l s ‚Üî
      ‚àÄ u : ‚Ñï ‚Üí Œπ, Tendsto u atTop l ‚Üí TendstoUniformlyOn (fun n => F (u n)) f atTop s :=
  ‚ü®TendstoUniformlyOn.seq_tendstoUniformlyOn, tendstoUniformlyOn_of_seq_tendstoUniformlyOn‚ü©

theorem tendstoUniformly_iff_seq_tendstoUniformly {l : Filter Œπ} [l.IsCountablyGenerated] :
    TendstoUniformly F f l ‚Üî
      ‚àÄ u : ‚Ñï ‚Üí Œπ, Tendsto u atTop l ‚Üí TendstoUniformly (fun n => F (u n)) f atTop := by
  simp_rw [‚Üê tendstoUniformlyOn_univ]
  exact tendstoUniformlyOn_iff_seq_tendstoUniformlyOn

end SeqTendsto

section

variable [NeBot p] {L : Œπ ‚Üí Œ≤} {‚Ñì : Œ≤}

theorem TendstoUniformlyOnFilter.tendsto_of_eventually_tendsto
    (h1 : TendstoUniformlyOnFilter F f p p') (h2 : ‚àÄ·∂† i in p, Tendsto (F i) p' (ùìù (L i)))
    (h3 : Tendsto L p (ùìù ‚Ñì)) : Tendsto f p' (ùìù ‚Ñì) := by
  rw [tendsto_nhds_left]
  intro s hs
  rw [mem_map, Set.preimage, ‚Üê eventually_iff]
  obtain ‚ü®t, ht, hts‚ü© := comp3_mem_uniformity hs
  have p1 : ‚àÄ·∂† i in p, (L i, ‚Ñì) ‚àà t := tendsto_nhds_left.mp h3 ht
  have p2 : ‚àÄ·∂† i in p, ‚àÄ·∂† x in p', (F i x, L i) ‚àà t := by
    filter_upwards [h2] with i h2 using tendsto_nhds_left.mp h2 ht
  have p3 : ‚àÄ·∂† i in p, ‚àÄ·∂† x in p', (f x, F i x) ‚àà t := (h1 t ht).curry
  obtain ‚ü®i, p4, p5, p6‚ü© := (p1.and (p2.and p3)).exists
  filter_upwards [p5, p6] with x p5 p6 using hts ‚ü®F i x, p6, L i, p5, p4‚ü©

theorem TendstoUniformly.tendsto_of_eventually_tendsto
    (h1 : TendstoUniformly F f p) (h2 : ‚àÄ·∂† i in p, Tendsto (F i) p' (ùìù (L i)))
    (h3 : Tendsto L p (ùìù ‚Ñì)) : Tendsto f p' (ùìù ‚Ñì) :=
  (h1.tendstoUniformlyOnFilter.mono_right le_top).tendsto_of_eventually_tendsto h2 h3

end

variable [TopologicalSpace Œ±]

/-- A sequence of functions `F‚Çô` converges locally uniformly on a set `s` to a limiting function
`f` with respect to a filter `p` if, for any entourage of the diagonal `u`, for any `x ‚àà s`, one
has `p`-eventually `(f y, F‚Çô y) ‚àà u` for all `y` in a neighborhood of `x` in `s`. -/
def TendstoLocallyUniformlyOn (F : Œπ ‚Üí Œ± ‚Üí Œ≤) (f : Œ± ‚Üí Œ≤) (p : Filter Œπ) (s : Set Œ±) :=
  ‚àÄ u ‚àà ùì§ Œ≤, ‚àÄ x ‚àà s, ‚àÉ t ‚àà ùìù[s] x, ‚àÄ·∂† n in p, ‚àÄ y ‚àà t, (f y, F n y) ‚àà u

/-- A sequence of functions `F‚Çô` converges locally uniformly to a limiting function `f` with respect
to a filter `p` if, for any entourage of the diagonal `u`, for any `x`, one has `p`-eventually
`(f y, F‚Çô y) ‚àà u` for all `y` in a neighborhood of `x`. -/
def TendstoLocallyUniformly (F : Œπ ‚Üí Œ± ‚Üí Œ≤) (f : Œ± ‚Üí Œ≤) (p : Filter Œπ) :=
  ‚àÄ u ‚àà ùì§ Œ≤, ‚àÄ x : Œ±, ‚àÉ t ‚àà ùìù x, ‚àÄ·∂† n in p, ‚àÄ y ‚àà t, (f y, F n y) ‚àà u

theorem tendstoLocallyUniformlyOn_univ :
    TendstoLocallyUniformlyOn F f p univ ‚Üî TendstoLocallyUniformly F f p := by
  simp [TendstoLocallyUniformlyOn, TendstoLocallyUniformly, nhdsWithin_univ]

theorem tendstoLocallyUniformlyOn_iff_forall_tendsto :
    TendstoLocallyUniformlyOn F f p s ‚Üî
      ‚àÄ x ‚àà s, Tendsto (fun y : Œπ √ó Œ± => (f y.2, F y.1 y.2)) (p √óÀ¢ ùìù[s] x) (ùì§ Œ≤) :=
  forall‚ÇÇ_swap.trans <| forall‚ÇÑ_congr fun _ _ _ _ => by
    rw [mem_map, mem_prod_iff_right]; rfl

nonrec theorem IsOpen.tendstoLocallyUniformlyOn_iff_forall_tendsto (hs : IsOpen s) :
    TendstoLocallyUniformlyOn F f p s ‚Üî
      ‚àÄ x ‚àà s, Tendsto (fun y : Œπ √ó Œ± => (f y.2, F y.1 y.2)) (p √óÀ¢ ùìù x) (ùì§ Œ≤) :=
  tendstoLocallyUniformlyOn_iff_forall_tendsto.trans <| forall‚ÇÇ_congr fun x hx => by
    rw [hs.nhdsWithin_eq hx]

theorem tendstoLocallyUniformly_iff_forall_tendsto :
    TendstoLocallyUniformly F f p ‚Üî
      ‚àÄ x, Tendsto (fun y : Œπ √ó Œ± => (f y.2, F y.1 y.2)) (p √óÀ¢ ùìù x) (ùì§ Œ≤) := by
  simp [‚Üê tendstoLocallyUniformlyOn_univ, isOpen_univ.tendstoLocallyUniformlyOn_iff_forall_tendsto]

theorem tendstoLocallyUniformlyOn_iff_tendstoLocallyUniformly_comp_coe :
    TendstoLocallyUniformlyOn F f p s ‚Üî
      TendstoLocallyUniformly (fun i (x : s) => F i x) (f ‚àò (‚Üë)) p := by
  simp only [tendstoLocallyUniformly_iff_forall_tendsto, Subtype.forall', tendsto_map'_iff,
    tendstoLocallyUniformlyOn_iff_forall_tendsto, ‚Üê map_nhds_subtype_val, prod_map_right]; rfl

protected theorem TendstoUniformlyOn.tendstoLocallyUniformlyOn (h : TendstoUniformlyOn F f p s) :
    TendstoLocallyUniformlyOn F f p s := fun u hu _ _ =>
  ‚ü®s, self_mem_nhdsWithin, by simpa using h u hu‚ü©

protected theorem TendstoUniformly.tendstoLocallyUniformly (h : TendstoUniformly F f p) :
    TendstoLocallyUniformly F f p := fun u hu _ => ‚ü®univ, univ_mem, by simpa using h u hu‚ü©

theorem TendstoLocallyUniformlyOn.mono (h : TendstoLocallyUniformlyOn F f p s) (h' : s' ‚äÜ s) :
    TendstoLocallyUniformlyOn F f p s' := by
  intro u hu x hx
  rcases h u hu x (h' hx) with ‚ü®t, ht, H‚ü©
  exact ‚ü®t, nhdsWithin_mono x h' ht, H.mono fun n => id‚ü©

-- Porting note: generalized from `Type` to `Sort`
theorem tendstoLocallyUniformlyOn_iUnion {Œπ' : Sort*} {S : Œπ' ‚Üí Set Œ±} (hS : ‚àÄ i, IsOpen (S i))
    (h : ‚àÄ i, TendstoLocallyUniformlyOn F f p (S i)) :
    TendstoLocallyUniformlyOn F f p (‚ãÉ i, S i) :=
  (isOpen_iUnion hS).tendstoLocallyUniformlyOn_iff_forall_tendsto.2 fun _x hx =>
    let ‚ü®i, hi‚ü© := mem_iUnion.1 hx
    (hS i).tendstoLocallyUniformlyOn_iff_forall_tendsto.1 (h i) _ hi

theorem tendstoLocallyUniformlyOn_biUnion {s : Set Œ≥} {S : Œ≥ ‚Üí Set Œ±} (hS : ‚àÄ i ‚àà s, IsOpen (S i))
    (h : ‚àÄ i ‚àà s, TendstoLocallyUniformlyOn F f p (S i)) :
    TendstoLocallyUniformlyOn F f p (‚ãÉ i ‚àà s, S i) :=
  tendstoLocallyUniformlyOn_iUnion (fun i => isOpen_iUnion (hS i)) fun i =>
   tendstoLocallyUniformlyOn_iUnion (hS i) (h i)

theorem tendstoLocallyUniformlyOn_sUnion (S : Set (Set Œ±)) (hS : ‚àÄ s ‚àà S, IsOpen s)
    (h : ‚àÄ s ‚àà S, TendstoLocallyUniformlyOn F f p s) : TendstoLocallyUniformlyOn F f p (‚ãÉ‚ÇÄ S) := by
  rw [sUnion_eq_biUnion]
  exact tendstoLocallyUniformlyOn_biUnion hS h

theorem TendstoLocallyUniformlyOn.union {s‚ÇÅ s‚ÇÇ : Set Œ±} (hs‚ÇÅ : IsOpen s‚ÇÅ) (hs‚ÇÇ : IsOpen s‚ÇÇ)
    (h‚ÇÅ : TendstoLocallyUniformlyOn F f p s‚ÇÅ) (h‚ÇÇ : TendstoLocallyUniformlyOn F f p s‚ÇÇ) :
    TendstoLocallyUniformlyOn F f p (s‚ÇÅ ‚à™ s‚ÇÇ) := by
  rw [‚Üê sUnion_pair]
  refine tendstoLocallyUniformlyOn_sUnion _ ?_ ?_ <;> simp [*]

-- Porting note: tendstoLocallyUniformlyOn_univ moved up

protected theorem TendstoLocallyUniformly.tendstoLocallyUniformlyOn
    (h : TendstoLocallyUniformly F f p) : TendstoLocallyUniformlyOn F f p s :=
  (tendstoLocallyUniformlyOn_univ.mpr h).mono (subset_univ _)

/-- On a compact space, locally uniform convergence is just uniform convergence. -/
theorem tendstoLocallyUniformly_iff_tendstoUniformly_of_compactSpace [CompactSpace Œ±] :
    TendstoLocallyUniformly F f p ‚Üî TendstoUniformly F f p := by
  refine ‚ü®fun h V hV => ?_, TendstoUniformly.tendstoLocallyUniformly‚ü©
  choose U hU using h V hV
  obtain ‚ü®t, ht‚ü© := isCompact_univ.elim_nhds_subcover' (fun k _ => U k) fun k _ => (hU k).1
  replace hU := fun x : t => (hU x).2
  rw [‚Üê eventually_all] at hU
  refine hU.mono fun i hi x => ?_
  specialize ht (mem_univ x)
  simp only [exists_prop, mem_iUnion, SetCoe.exists, exists_and_right, Subtype.coe_mk] at ht
  obtain ‚ü®y, ‚ü®hy‚ÇÅ, hy‚ÇÇ‚ü©, hy‚ÇÉ‚ü© := ht
  exact hi ‚ü®‚ü®y, hy‚ÇÅ‚ü©, hy‚ÇÇ‚ü© x hy‚ÇÉ

/-- For a compact set `s`, locally uniform convergence on `s` is just uniform convergence on `s`. -/
theorem tendstoLocallyUniformlyOn_iff_tendstoUniformlyOn_of_compact (hs : IsCompact s) :
    TendstoLocallyUniformlyOn F f p s ‚Üî TendstoUniformlyOn F f p s := by
  haveI : CompactSpace s := isCompact_iff_compactSpace.mp hs
  refine ‚ü®fun h => ?_, TendstoUniformlyOn.tendstoLocallyUniformlyOn‚ü©
  rwa [tendstoLocallyUniformlyOn_iff_tendstoLocallyUniformly_comp_coe,
    tendstoLocallyUniformly_iff_tendstoUniformly_of_compactSpace, ‚Üê
    tendstoUniformlyOn_iff_tendstoUniformly_comp_coe] at h

theorem TendstoLocallyUniformlyOn.comp [TopologicalSpace Œ≥] {t : Set Œ≥}
    (h : TendstoLocallyUniformlyOn F f p s) (g : Œ≥ ‚Üí Œ±) (hg : MapsTo g t s)
    (cg : ContinuousOn g t) : TendstoLocallyUniformlyOn (fun n => F n ‚àò g) (f ‚àò g) p t := by
  intro u hu x hx
  rcases h u hu (g x) (hg hx) with ‚ü®a, ha, H‚ü©
  have : g ‚Åª¬π' a ‚àà ùìù[t] x :=
    (cg x hx).preimage_mem_nhdsWithin' (nhdsWithin_mono (g x) hg.image_subset ha)
  exact ‚ü®g ‚Åª¬π' a, this, H.mono fun n hn y hy => hn _ hy‚ü©

theorem TendstoLocallyUniformly.comp [TopologicalSpace Œ≥] (h : TendstoLocallyUniformly F f p)
    (g : Œ≥ ‚Üí Œ±) (cg : Continuous g) : TendstoLocallyUniformly (fun n => F n ‚àò g) (f ‚àò g) p := by
  rw [‚Üê tendstoLocallyUniformlyOn_univ] at h ‚ä¢
  rw [continuous_iff_continuousOn_univ] at cg
  exact h.comp _ (mapsTo_univ _ _) cg

theorem tendstoLocallyUniformlyOn_TFAE [LocallyCompactSpace Œ±] (G : Œπ ‚Üí Œ± ‚Üí Œ≤) (g : Œ± ‚Üí Œ≤)
    (p : Filter Œπ) (hs : IsOpen s) :
    List.TFAE [
      TendstoLocallyUniformlyOn G g p s,
      ‚àÄ K, K ‚äÜ s ‚Üí IsCompact K ‚Üí TendstoUniformlyOn G g p K,
      ‚àÄ x ‚àà s, ‚àÉ v ‚àà ùìù[s] x, TendstoUniformlyOn G g p v] := by
  tfae_have 1 ‚Üí 2
  | h, K, hK1, hK2 =>
    (tendstoLocallyUniformlyOn_iff_tendstoUniformlyOn_of_compact hK2).mp (h.mono hK1)
  tfae_have 2 ‚Üí 3
  | h, x, hx => by
    obtain ‚ü®K, ‚ü®hK1, hK2‚ü©, hK3‚ü© := (compact_basis_nhds x).mem_iff.mp (hs.mem_nhds hx)
    exact ‚ü®K, nhdsWithin_le_nhds hK1, h K hK3 hK2‚ü©
  tfae_have 3 ‚Üí 1
  | h, u, hu, x, hx => by
    obtain ‚ü®v, hv1, hv2‚ü© := h x hx
    exact ‚ü®v, hv1, hv2 u hu‚ü©
  tfae_finish

theorem tendstoLocallyUniformlyOn_iff_forall_isCompact [LocallyCompactSpace Œ±] (hs : IsOpen s) :
    TendstoLocallyUniformlyOn F f p s ‚Üî ‚àÄ K, K ‚äÜ s ‚Üí IsCompact K ‚Üí TendstoUniformlyOn F f p K :=
  (tendstoLocallyUniformlyOn_TFAE F f p hs).out 0 1

lemma tendstoLocallyUniformly_iff_forall_isCompact [LocallyCompactSpace Œ±]  :
    TendstoLocallyUniformly F f p ‚Üî ‚àÄ K : Set Œ±, IsCompact K ‚Üí TendstoUniformlyOn F f p K := by
  simp only [‚Üê tendstoLocallyUniformlyOn_univ,
    tendstoLocallyUniformlyOn_iff_forall_isCompact isOpen_univ, Set.subset_univ, forall_true_left]

theorem tendstoLocallyUniformlyOn_iff_filter :
    TendstoLocallyUniformlyOn F f p s ‚Üî ‚àÄ x ‚àà s, TendstoUniformlyOnFilter F f p (ùìù[s] x) := by
  simp only [TendstoUniformlyOnFilter, eventually_prod_iff]
  constructor
  ¬∑ rintro h x hx u hu
    obtain ‚ü®s, hs1, hs2‚ü© := h u hu x hx
    exact ‚ü®_, hs2, _, eventually_of_mem hs1 fun x => id, fun hi y hy => hi y hy‚ü©
  ¬∑ rintro h u hu x hx
    obtain ‚ü®pa, hpa, pb, hpb, h‚ü© := h x hx u hu
    exact ‚ü®pb, hpb, eventually_of_mem hpa fun i hi y hy => h hi hy‚ü©

theorem tendstoLocallyUniformly_iff_filter :
    TendstoLocallyUniformly F f p ‚Üî ‚àÄ x, TendstoUniformlyOnFilter F f p (ùìù x) := by
  simpa [‚Üê tendstoLocallyUniformlyOn_univ, ‚Üê nhdsWithin_univ] using
    @tendstoLocallyUniformlyOn_iff_filter _ _ _ _ F f univ p _

theorem TendstoLocallyUniformlyOn.tendsto_at (hf : TendstoLocallyUniformlyOn F f p s) {a : Œ±}
    (ha : a ‚àà s) : Tendsto (fun i => F i a) p (ùìù (f a)) := by
  refine ((tendstoLocallyUniformlyOn_iff_filter.mp hf) a ha).tendsto_at ?_
  simpa only [Filter.principal_singleton] using pure_le_nhdsWithin ha

theorem TendstoLocallyUniformlyOn.unique [p.NeBot] [T2Space Œ≤] {g : Œ± ‚Üí Œ≤}
    (hf : TendstoLocallyUniformlyOn F f p s) (hg : TendstoLocallyUniformlyOn F g p s) :
    s.EqOn f g := fun _a ha => tendsto_nhds_unique (hf.tendsto_at ha) (hg.tendsto_at ha)

theorem TendstoLocallyUniformlyOn.congr {G : Œπ ‚Üí Œ± ‚Üí Œ≤} (hf : TendstoLocallyUniformlyOn F f p s)
    (hg : ‚àÄ n, s.EqOn (F n) (G n)) : TendstoLocallyUniformlyOn G f p s := by
  rintro u hu x hx
  obtain ‚ü®t, ht, h‚ü© := hf u hu x hx
  refine ‚ü®s ‚à© t, inter_mem self_mem_nhdsWithin ht, ?_‚ü©
  filter_upwards [h] with i hi y hy using hg i hy.1 ‚ñ∏ hi y hy.2

theorem TendstoLocallyUniformlyOn.congr_right {g : Œ± ‚Üí Œ≤} (hf : TendstoLocallyUniformlyOn F f p s)
    (hg : s.EqOn f g) : TendstoLocallyUniformlyOn F g p s := by
  rintro u hu x hx
  obtain ‚ü®t, ht, h‚ü© := hf u hu x hx
  refine ‚ü®s ‚à© t, inter_mem self_mem_nhdsWithin ht, ?_‚ü©
  filter_upwards [h] with i hi y hy using hg hy.1 ‚ñ∏ hi y hy.2

/-!
### Uniform approximation

In this section, we give lemmas ensuring that a function is continuous if it can be approximated
uniformly by continuous functions. We give various versions, within a set or the whole space, at
a single point or at all points, with locally uniform approximation or uniform approximation. All
the statements are derived from a statement about locally uniform approximation within a set at
a point, called `continuousWithinAt_of_locally_uniform_approx_of_continuousWithinAt`. -/


/-- A function which can be locally uniformly approximated by functions which are continuous
within a set at a point is continuous within this set at this point. -/
theorem continuousWithinAt_of_locally_uniform_approx_of_continuousWithinAt (hx : x ‚àà s)
    (L : ‚àÄ u ‚àà ùì§ Œ≤, ‚àÉ t ‚àà ùìù[s] x, ‚àÉ F : Œ± ‚Üí Œ≤, ContinuousWithinAt F s x ‚àß ‚àÄ y ‚àà t, (f y, F y) ‚àà u) :
    ContinuousWithinAt f s x := by
  refine Uniform.continuousWithinAt_iff'_left.2 fun u‚ÇÄ hu‚ÇÄ => ?_
  obtain ‚ü®u‚ÇÅ, h‚ÇÅ, u‚ÇÅ‚ÇÄ‚ü© : ‚àÉ u ‚àà ùì§ Œ≤, u ‚óã u ‚äÜ u‚ÇÄ := comp_mem_uniformity_sets hu‚ÇÄ
  obtain ‚ü®u‚ÇÇ, h‚ÇÇ, hsymm, u‚ÇÇ‚ÇÅ‚ü© : ‚àÉ u ‚àà ùì§ Œ≤, (‚àÄ {a b}, (a, b) ‚àà u ‚Üí (b, a) ‚àà u) ‚àß u ‚óã u ‚äÜ u‚ÇÅ :=
    comp_symm_of_uniformity h‚ÇÅ
  rcases L u‚ÇÇ h‚ÇÇ with ‚ü®t, tx, F, hFc, hF‚ü©
  have A : ‚àÄ·∂† y in ùìù[s] x, (f y, F y) ‚àà u‚ÇÇ := Eventually.mono tx hF
  have B : ‚àÄ·∂† y in ùìù[s] x, (F y, F x) ‚àà u‚ÇÇ := Uniform.continuousWithinAt_iff'_left.1 hFc h‚ÇÇ
  have C : ‚àÄ·∂† y in ùìù[s] x, (f y, F x) ‚àà u‚ÇÅ :=
    (A.and B).mono fun y hy => u‚ÇÇ‚ÇÅ (prod_mk_mem_compRel hy.1 hy.2)
  have : (F x, f x) ‚àà u‚ÇÅ :=
    u‚ÇÇ‚ÇÅ (prod_mk_mem_compRel (refl_mem_uniformity h‚ÇÇ) (hsymm (A.self_of_nhdsWithin hx)))
  exact C.mono fun y hy => u‚ÇÅ‚ÇÄ (prod_mk_mem_compRel hy this)

/-- A function which can be locally uniformly approximated by functions which are continuous at
a point is continuous at this point. -/
theorem continuousAt_of_locally_uniform_approx_of_continuousAt
    (L : ‚àÄ u ‚àà ùì§ Œ≤, ‚àÉ t ‚àà ùìù x, ‚àÉ F, ContinuousAt F x ‚àß ‚àÄ y ‚àà t, (f y, F y) ‚àà u) :
    ContinuousAt f x := by
  rw [‚Üê continuousWithinAt_univ]
  apply continuousWithinAt_of_locally_uniform_approx_of_continuousWithinAt (mem_univ _) _
  simpa only [exists_prop, nhdsWithin_univ, continuousWithinAt_univ] using L

/-- A function which can be locally uniformly approximated by functions which are continuous
on a set is continuous on this set. -/
theorem continuousOn_of_locally_uniform_approx_of_continuousWithinAt
    (L : ‚àÄ x ‚àà s, ‚àÄ u ‚àà ùì§ Œ≤, ‚àÉ t ‚àà ùìù[s] x, ‚àÉ F,
      ContinuousWithinAt F s x ‚àß ‚àÄ y ‚àà t, (f y, F y) ‚àà u) :
    ContinuousOn f s := fun x hx =>
  continuousWithinAt_of_locally_uniform_approx_of_continuousWithinAt hx (L x hx)

/-- A function which can be uniformly approximated by functions which are continuous on a set
is continuous on this set. -/
theorem continuousOn_of_uniform_approx_of_continuousOn
    (L : ‚àÄ u ‚àà ùì§ Œ≤, ‚àÉ F, ContinuousOn F s ‚àß ‚àÄ y ‚àà s, (f y, F y) ‚àà u) : ContinuousOn f s :=
  continuousOn_of_locally_uniform_approx_of_continuousWithinAt fun _x hx u hu =>
    ‚ü®s, self_mem_nhdsWithin, (L u hu).imp fun _F hF => ‚ü®hF.1.continuousWithinAt hx, hF.2‚ü©‚ü©

/-- A function which can be locally uniformly approximated by continuous functions is continuous. -/
theorem continuous_of_locally_uniform_approx_of_continuousAt
    (L : ‚àÄ x : Œ±, ‚àÄ u ‚àà ùì§ Œ≤, ‚àÉ t ‚àà ùìù x, ‚àÉ F, ContinuousAt F x ‚àß ‚àÄ y ‚àà t, (f y, F y) ‚àà u) :
    Continuous f :=
  continuous_iff_continuousAt.2 fun x =>
    continuousAt_of_locally_uniform_approx_of_continuousAt (L x)

/-- A function which can be uniformly approximated by continuous functions is continuous. -/
theorem continuous_of_uniform_approx_of_continuous
    (L : ‚àÄ u ‚àà ùì§ Œ≤, ‚àÉ F, Continuous F ‚àß ‚àÄ y, (f y, F y) ‚àà u) : Continuous f :=
  continuous_iff_continuousOn_univ.mpr <|
    continuousOn_of_uniform_approx_of_continuousOn <| by
      simpa [continuous_iff_continuousOn_univ] using L

/-!
### Uniform limits

From the previous statements on uniform approximation, we deduce continuity results for uniform
limits.
-/


/-- A locally uniform limit on a set of functions which are continuous on this set is itself
continuous on this set. -/
protected theorem TendstoLocallyUniformlyOn.continuousOn (h : TendstoLocallyUniformlyOn F f p s)
    (hc : ‚àÄ·∂† n in p, ContinuousOn (F n) s) [NeBot p] : ContinuousOn f s := by
  refine continuousOn_of_locally_uniform_approx_of_continuousWithinAt fun x hx u hu => ?_
  rcases h u hu x hx with ‚ü®t, ht, H‚ü©
  rcases (hc.and H).exists with ‚ü®n, hFc, hF‚ü©
  exact ‚ü®t, ht, ‚ü®F n, hFc.continuousWithinAt hx, hF‚ü©‚ü©

/-- A uniform limit on a set of functions which are continuous on this set is itself continuous
on this set. -/
protected theorem TendstoUniformlyOn.continuousOn (h : TendstoUniformlyOn F f p s)
    (hc : ‚àÄ·∂† n in p, ContinuousOn (F n) s) [NeBot p] : ContinuousOn f s :=
  h.tendstoLocallyUniformlyOn.continuousOn hc

/-- A locally uniform limit of continuous functions is continuous. -/
protected theorem TendstoLocallyUniformly.continuous (h : TendstoLocallyUniformly F f p)
    (hc : ‚àÄ·∂† n in p, Continuous (F n)) [NeBot p] : Continuous f :=
  continuous_iff_continuousOn_univ.mpr <|
    h.tendstoLocallyUniformlyOn.continuousOn <| hc.mono fun _n hn => hn.continuousOn

/-- A uniform limit of continuous functions is continuous. -/
protected theorem TendstoUniformly.continuous (h : TendstoUniformly F f p)
    (hc : ‚àÄ·∂† n in p, Continuous (F n)) [NeBot p] : Continuous f :=
  h.tendstoLocallyUniformly.continuous hc

/-!
### Composing limits under uniform convergence

In general, if `F‚Çô` converges pointwise to a function `f`, and `g‚Çô` tends to `x`, it is not true
that `F‚Çô g‚Çô` tends to `f x`. It is true however if the convergence of `F‚Çô` to `f` is uniform. In
this paragraph, we prove variations around this statement.
-/


/-- If `F‚Çô` converges locally uniformly on a neighborhood of `x` within a set `s` to a function `f`
which is continuous at `x` within `s`, and `g‚Çô` tends to `x` within `s`, then `F‚Çô (g‚Çô)` tends
to `f x`. -/
theorem tendsto_comp_of_locally_uniform_limit_within (h : ContinuousWithinAt f s x)
    (hg : Tendsto g p (ùìù[s] x))
    (hunif : ‚àÄ u ‚àà ùì§ Œ≤, ‚àÉ t ‚àà ùìù[s] x, ‚àÄ·∂† n in p, ‚àÄ y ‚àà t, (f y, F n y) ‚àà u) :
    Tendsto (fun n => F n (g n)) p (ùìù (f x)) := by
  refine Uniform.tendsto_nhds_right.2 fun u‚ÇÄ hu‚ÇÄ => ?_
  obtain ‚ü®u‚ÇÅ, h‚ÇÅ, u‚ÇÅ‚ÇÄ‚ü© : ‚àÉ u ‚àà ùì§ Œ≤, u ‚óã u ‚äÜ u‚ÇÄ := comp_mem_uniformity_sets hu‚ÇÄ
  rcases hunif u‚ÇÅ h‚ÇÅ with ‚ü®s, sx, hs‚ü©
  have A : ‚àÄ·∂† n in p, g n ‚àà s := hg sx
  have B : ‚àÄ·∂† n in p, (f x, f (g n)) ‚àà u‚ÇÅ := hg (Uniform.continuousWithinAt_iff'_right.1 h h‚ÇÅ)
  exact B.mp <| A.mp <| hs.mono fun y H1 H2 H3 => u‚ÇÅ‚ÇÄ (prod_mk_mem_compRel H3 (H1 _ H2))

/-- If `F‚Çô` converges locally uniformly on a neighborhood of `x` to a function `f` which is
continuous at `x`, and `g‚Çô` tends to `x`, then `F‚Çô (g‚Çô)` tends to `f x`. -/
theorem tendsto_comp_of_locally_uniform_limit (h : ContinuousAt f x) (hg : Tendsto g p (ùìù x))
    (hunif : ‚àÄ u ‚àà ùì§ Œ≤, ‚àÉ t ‚àà ùìù x, ‚àÄ·∂† n in p, ‚àÄ y ‚àà t, (f y, F n y) ‚àà u) :
    Tendsto (fun n => F n (g n)) p (ùìù (f x)) := by
  rw [‚Üê continuousWithinAt_univ] at h
  rw [‚Üê nhdsWithin_univ] at hunif hg
  exact tendsto_comp_of_locally_uniform_limit_within h hg hunif

/-- If `F‚Çô` tends locally uniformly to `f` on a set `s`, and `g‚Çô` tends to `x` within `s`, then
`F‚Çô g‚Çô` tends to `f x` if `f` is continuous at `x` within `s` and `x ‚àà s`. -/
theorem TendstoLocallyUniformlyOn.tendsto_comp (h : TendstoLocallyUniformlyOn F f p s)
    (hf : ContinuousWithinAt f s x) (hx : x ‚àà s) (hg : Tendsto g p (ùìù[s] x)) :
    Tendsto (fun n => F n (g n)) p (ùìù (f x)) :=
  tendsto_comp_of_locally_uniform_limit_within hf hg fun u hu => h u hu x hx

/-- If `F‚Çô` tends uniformly to `f` on a set `s`, and `g‚Çô` tends to `x` within `s`, then `F‚Çô g‚Çô`
tends to `f x` if `f` is continuous at `x` within `s`. -/
theorem TendstoUniformlyOn.tendsto_comp (h : TendstoUniformlyOn F f p s)
    (hf : ContinuousWithinAt f s x) (hg : Tendsto g p (ùìù[s] x)) :
    Tendsto (fun n => F n (g n)) p (ùìù (f x)) :=
  tendsto_comp_of_locally_uniform_limit_within hf hg fun u hu => ‚ü®s, self_mem_nhdsWithin, h u hu‚ü©

/-- If `F‚Çô` tends locally uniformly to `f`, and `g‚Çô` tends to `x`, then `F‚Çô g‚Çô` tends to `f x`. -/
theorem TendstoLocallyUniformly.tendsto_comp (h : TendstoLocallyUniformly F f p)
    (hf : ContinuousAt f x) (hg : Tendsto g p (ùìù x)) : Tendsto (fun n => F n (g n)) p (ùìù (f x)) :=
  tendsto_comp_of_locally_uniform_limit hf hg fun u hu => h u hu x

/-- If `F‚Çô` tends uniformly to `f`, and `g‚Çô` tends to `x`, then `F‚Çô g‚Çô` tends to `f x`. -/
theorem TendstoUniformly.tendsto_comp (h : TendstoUniformly F f p) (hf : ContinuousAt f x)
    (hg : Tendsto g p (ùìù x)) : Tendsto (fun n => F n (g n)) p (ùìù (f x)) :=
  h.tendstoLocallyUniformly.tendsto_comp hf hg
