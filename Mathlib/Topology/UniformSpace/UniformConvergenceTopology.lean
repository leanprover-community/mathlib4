/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import Mathlib.Topology.UniformSpace.UniformConvergence
import Mathlib.Topology.UniformSpace.Pi
import Mathlib.Topology.UniformSpace.Equiv

#align_import topology.uniform_space.uniform_convergence_topology from "leanprover-community/mathlib"@"98e83c3d541c77cdb7da20d79611a780ff8e7d90"

/-!
# Topology and uniform structure of uniform convergence

This files endows `Î± â†’ Î²` with the topologies / uniform structures of
- uniform convergence on `Î±`
- uniform convergence on a specified family `ğ”–` of sets of `Î±`, also called `ğ”–`-convergence

Since `Î± â†’ Î²` is already endowed with the topologies and uniform structures of pointwise
convergence, we introduce type aliases `UniformFun Î± Î²` (denoted `Î± â†’áµ¤ Î²`) and
`UniformOnFun Î± Î² ğ”–` (denoted `Î± â†’áµ¤[ğ”–] Î²`) and we actually endow *these* with the structures
of uniform and `ğ”–`-convergence respectively.

Usual examples of the second construction include :
- the topology of compact convergence, when `ğ”–` is the set of compacts of `Î±`
- the strong topology on the dual of a topological vector space (TVS) `E`, when `ğ”–` is the set of
  Von Neumann bounded subsets of `E`
- the weak-* topology on the dual of a TVS `E`, when `ğ”–` is the set of singletons of `E`.

This file contains a lot of technical facts, so it is heavily commented, proofs included!

## Main definitions

* `UniformFun.gen`: basis sets for the uniformity of uniform convergence. These are sets
  of the form `S(V) := {(f, g) | âˆ€ x : Î±, (f x, g x) âˆˆ V}` for some `V : Set (Î² Ã— Î²)`
* `UniformFun.uniformSpace`: uniform structure of uniform convergence. This is the
  `UniformSpace` on `Î± â†’áµ¤ Î²` whose uniformity is generated by the sets `S(V)` for `V âˆˆ ğ“¤ Î²`.
  We will denote this uniform space as `ğ’°(Î±, Î², uÎ²)`, both in the comments and as a local notation
  in the Lean code, where `uÎ²` is the uniform space structure on `Î²`.
  This is declared as an instance on `Î± â†’áµ¤ Î²`.
* `UniformOnFun.uniformSpace`: uniform structure of `ğ”–`-convergence, where
  `ğ”– : Set (Set Î±)`. This is the infimum, for `S âˆˆ ğ”–`, of the pullback of `ğ’° S Î²` by the map of
  restriction to `S`. We will denote it `ğ’±(Î±, Î², ğ”–, uÎ²)`, where `uÎ²` is the uniform space structure
  on `Î²`.
  This is declared as an instance on `Î± â†’áµ¤[ğ”–] Î²`.

## Main statements

### Basic properties

* `UniformFun.uniformContinuous_eval`: evaluation is uniformly continuous on `Î± â†’áµ¤ Î²`.
* `UniformFun.t2Space`: the topology of uniform convergence on `Î± â†’áµ¤ Î²` is Tâ‚‚ if
  `Î²` is Tâ‚‚.
* `UniformFun.tendsto_iff_tendstoUniformly`: `ğ’°(Î±, Î², uÎ²)` is
  indeed the uniform structure of uniform convergence
* `UniformOnFun.uniformContinuous_eval_of_mem`: evaluation at a point contained in a
  set of `ğ”–` is uniformly continuous on `Î± â†’áµ¤[ğ”–] Î²`
* `UniformOnFun.t2Space_of_covering`: the topology of `ğ”–`-convergence on `Î± â†’áµ¤[ğ”–] Î²` is Tâ‚‚ if
  `Î²` is Tâ‚‚ and `ğ”–` covers `Î±`
* `UniformOnFun.tendsto_iff_tendstoUniformlyOn`:
  `ğ’±(Î±, Î², ğ”– uÎ²)` is indeed the uniform structure of `ğ”–`-convergence

### Functoriality and compatibility with product of uniform spaces

In order to avoid the need for filter bases as much as possible when using these definitions,
we develop an extensive API for manipulating these structures abstractly. As usual in the topology
section of mathlib, we first state results about the complete lattices of `UniformSpace`s on
fixed types, and then we use these to deduce categorical-like results about maps between two
uniform spaces.

We only describe these in the harder case of `ğ”–`-convergence, as the names of the corresponding
results for uniform convergence can easily be guessed.

#### Order statements

* `UniformOnFun.mono`: let `uâ‚`, `uâ‚‚` be two uniform structures on `Î³` and
  `ğ”–â‚ ğ”–â‚‚ : Set (Set Î±)`. If `uâ‚ â‰¤ uâ‚‚` and `ğ”–â‚‚ âŠ† ğ”–â‚` then `ğ’±(Î±, Î³, ğ”–â‚, uâ‚) â‰¤ ğ’±(Î±, Î³, ğ”–â‚‚, uâ‚‚)`.
* `UniformOnFun.iInf_eq`: if `u` is a family of uniform structures on `Î³`, then
  `ğ’±(Î±, Î³, ğ”–, (â¨… i, u i)) = â¨… i, ğ’±(Î±, Î³, ğ”–, u i)`.
* `UniformOnFun.comap_eq`: if `u` is a uniform structures on `Î²` and `f : Î³ â†’ Î²`, then
  `ğ’±(Î±, Î³, ğ”–, comap f u) = comap (fun g â†¦ f âˆ˜ g) ğ’±(Î±, Î³, ğ”–, uâ‚)`.

An interesting note about these statements is that they are proved without ever unfolding the basis
definition of the uniform structure of uniform convergence! Instead, we build a
(not very interesting) Galois connection `UniformFun.gc` and then rely on the Galois
connection API to do most of the work.

#### Morphism statements (unbundled)

* `UniformOnFun.postcomp_uniformContinuous`: if `f : Î³ â†’ Î²` is uniformly
  continuous, then `(fun g â†¦ f âˆ˜ g) : (Î± â†’áµ¤[ğ”–] Î³) â†’ (Î± â†’áµ¤[ğ”–] Î²)` is uniformly continuous.
* `UniformOnFun.postcomp_uniformInducing`: if `f : Î³ â†’ Î²` is a uniform
  inducing, then `(fun g â†¦ f âˆ˜ g) : (Î± â†’áµ¤[ğ”–] Î³) â†’ (Î± â†’áµ¤[ğ”–] Î²)` is a uniform inducing.
* `UniformOnFun.precomp_uniformContinuous`: let `f : Î³ â†’ Î±`, `ğ”– : Set (Set Î±)`,
  `ğ”— : Set (Set Î³)`, and assume that `âˆ€ T âˆˆ ğ”—, f '' T âˆˆ ğ”–`. Then, the function
  `(fun g â†¦ g âˆ˜ f) : (Î± â†’áµ¤[ğ”–] Î²) â†’ (Î³ â†’áµ¤[ğ”—] Î²)` is uniformly continuous.

#### Isomorphism statements (bundled)

* `UniformOnFun.congrRight`: turn a uniform isomorphism `Î³ â‰ƒáµ¤ Î²` into a uniform isomorphism
  `(Î± â†’áµ¤[ğ”–] Î³) â‰ƒáµ¤ (Î± â†’áµ¤[ğ”–] Î²)` by post-composing.
* `UniformOnFun.congrLeft`: turn a bijection `e : Î³ â‰ƒ Î±` such that we have both
  `âˆ€ T âˆˆ ğ”—, e '' T âˆˆ ğ”–` and `âˆ€ S âˆˆ ğ”–, e â»Â¹' S âˆˆ ğ”—` into a uniform isomorphism
  `(Î³ â†’áµ¤[ğ”—] Î²) â‰ƒáµ¤ (Î± â†’áµ¤[ğ”–] Î²)` by pre-composing.
* `UniformOnFun.uniformEquivPiComm`: the natural bijection between `Î± â†’ Î  i, Î´ i`
  and `Î  i, Î± â†’ Î´ i`, upgraded to a uniform isomorphism between `Î± â†’áµ¤[ğ”–] (Î  i, Î´ i)` and
  `Î  i, Î± â†’áµ¤[ğ”–] Î´ i`.

#### Important use cases

* If `G` is a uniform group, then `Î± â†’áµ¤[ğ”–] G` is a uniform group: since `(/) : G Ã— G â†’ G` is
  uniformly continuous, `UniformOnFun.postcomp_uniformContinuous` tells us that
  `((/) âˆ˜ â€”) : (Î± â†’áµ¤[ğ”–] G Ã— G) â†’ (Î± â†’áµ¤[ğ”–] G)` is uniformly continuous. By precomposing with
  `UniformOnFun.uniformEquivProdArrow`, this gives that
  `(/) : (Î± â†’áµ¤[ğ”–] G) Ã— (Î± â†’áµ¤[ğ”–] G) â†’ (Î± â†’áµ¤[ğ”–] G)` is also uniformly continuous
* The transpose of a continuous linear map is continuous for the strong topologies: since
  continuous linear maps are uniformly continuous and map bounded sets to bounded sets,
  this is just a special case of `UniformOnFun.precomp_uniformContinuous`.

## TODO

* Show that the uniform structure of `ğ”–`-convergence is exactly the structure of `ğ”–'`-convergence,
  where `ğ”–'` is the ***noncovering*** bornology (i.e ***not*** what `Bornology` currently refers
  to in mathlib) generated by `ğ”–`.

## References

* [N. Bourbaki, *General Topology, Chapter X*][bourbaki1966]

## Tags

uniform convergence
-/


noncomputable section

open Topology Classical Uniformity Filter

open Set Filter

section TypeAlias

/-- The type of functions from `Î±` to `Î²` equipped with the uniform structure and topology of
uniform convergence. We denote it `Î± â†’áµ¤ Î²`. -/
def UniformFun (Î± Î² : Type*) :=
  Î± â†’ Î²
#align uniform_fun UniformFun

/-- The type of functions from `Î±` to `Î²` equipped with the uniform structure and topology of
uniform convergence on some family `ğ”–` of subsets of `Î±`. We denote it `Î± â†’áµ¤[ğ”–] Î²`. -/
@[nolint unusedArguments]
def UniformOnFun (Î± Î² : Type*) (_ : Set (Set Î±)) :=
  Î± â†’ Î²
#align uniform_on_fun UniformOnFun

scoped[UniformConvergence] notation:25 Î± " â†’áµ¤ " Î²:0 => UniformFun Î± Î²

scoped[UniformConvergence] notation:25 Î± " â†’áµ¤[" ğ”– "] " Î²:0 => UniformOnFun Î± Î² ğ”–

-- Porting note: these are not used anymore
-- scoped[UniformConvergence] notation3 "Î»áµ˜ "(...)", "r:(scoped p => UniformFun.ofFun p) => r

-- scoped[UniformConvergence] notation3 "Î»áµ˜["ğ”–"] "(...)", "r:(scoped p => UniformFun.ofFun p) => r

open UniformConvergence

instance {Î± Î²} [Nonempty Î²] : Nonempty (Î± â†’áµ¤ Î²) :=
  Pi.Nonempty

instance {Î± Î² ğ”–} [Nonempty Î²] : Nonempty (Î± â†’áµ¤[ğ”–] Î²) :=
  Pi.Nonempty

/-- Reinterpret `f : Î± â†’ Î²` as an element of `Î± â†’áµ¤ Î²`. -/
def UniformFun.ofFun {Î± Î²} : (Î± â†’ Î²) â‰ƒ (Î± â†’áµ¤ Î²) :=
  âŸ¨fun x => x, fun x => x, fun _ => rfl, fun _ => rflâŸ©
#align uniform_fun.of_fun UniformFun.ofFun

/-- Reinterpret `f : Î± â†’ Î²` as an element of `Î± â†’áµ¤[ğ”–] Î²`. -/
def UniformOnFun.ofFun {Î± Î²} (ğ”–) : (Î± â†’ Î²) â‰ƒ (Î± â†’áµ¤[ğ”–] Î²) :=
  âŸ¨fun x => x, fun x => x, fun _ => rfl, fun _ => rflâŸ©
#align uniform_on_fun.of_fun UniformOnFun.ofFun

/-- Reinterpret `f : Î± â†’áµ¤ Î²` as an element of `Î± â†’ Î²`. -/
def UniformFun.toFun {Î± Î²} : (Î± â†’áµ¤ Î²) â‰ƒ (Î± â†’ Î²) :=
  UniformFun.ofFun.symm
#align uniform_fun.to_fun UniformFun.toFun

/-- Reinterpret `f : Î± â†’áµ¤[ğ”–] Î²` as an element of `Î± â†’ Î²`. -/
def UniformOnFun.toFun {Î± Î²} (ğ”–) : (Î± â†’áµ¤[ğ”–] Î²) â‰ƒ (Î± â†’ Î²) :=
  (UniformOnFun.ofFun ğ”–).symm
#align uniform_on_fun.to_fun UniformOnFun.toFun

-- Note: we don't declare a `CoeFun` instance because Lean wouldn't insert it when writing
-- `f x` (because of definitional equality with `Î± â†’ Î²`).
end TypeAlias

open UniformConvergence

namespace UniformFun

variable (Î± Î² : Type*) {Î³ Î¹ : Type*}

variable {s s' : Set Î±} {x : Î±} {p : Filter Î¹} {g : Î¹ â†’ Î±}

/-- Basis sets for the uniformity of uniform convergence: `gen Î± Î² V` is the set of pairs `(f, g)`
of functions `Î± â†’áµ¤ Î²` such that `âˆ€ x, (f x, g x) âˆˆ V`. -/
protected def gen (V : Set (Î² Ã— Î²)) : Set ((Î± â†’áµ¤ Î²) Ã— (Î± â†’áµ¤ Î²)) :=
  { uv : (Î± â†’áµ¤ Î²) Ã— (Î± â†’áµ¤ Î²) | âˆ€ x, (uv.1 x, uv.2 x) âˆˆ V }
#align uniform_fun.gen UniformFun.gen

/-- If `ğ“•` is a filter on `Î² Ã— Î²`, then the set of all `UniformFun.gen Î± Î² V` for
`V âˆˆ ğ“•` is a filter basis on `(Î± â†’áµ¤ Î²) Ã— (Î± â†’áµ¤ Î²)`. This will only be applied to `ğ“• = ğ“¤ Î²` when
`Î²` is equipped with a `UniformSpace` structure, but it is useful to define it for any filter in
order to be able to state that it has a lower adjoint (see `UniformFun.gc`). -/
protected theorem isBasis_gen (ğ“‘ : Filter <| Î² Ã— Î²) :
    IsBasis (fun V : Set (Î² Ã— Î²) => V âˆˆ ğ“‘) (UniformFun.gen Î± Î²) :=
  âŸ¨âŸ¨univ, univ_memâŸ©, @fun U V hU hV =>
    âŸ¨U âˆ© V, inter_mem hU hV, fun _ huv => âŸ¨fun x => (huv x).left, fun x => (huv x).rightâŸ©âŸ©âŸ©
#align uniform_fun.is_basis_gen UniformFun.isBasis_gen

/-- For `ğ“• : Filter (Î² Ã— Î²)`, this is the set of all `UniformFun.gen Î± Î² V` for
`V âˆˆ ğ“•` as a bundled `FilterBasis` over `(Î± â†’áµ¤ Î²) Ã— (Î± â†’áµ¤ Î²)`. This will only be applied to
`ğ“• = ğ“¤ Î²` when `Î²` is equipped with a `UniformSpace` structure, but it is useful to define it for
any filter in order to be able to state that it has a lower adjoint
(see `UniformFun.gc`). -/
protected def basis (ğ“• : Filter <| Î² Ã— Î²) : FilterBasis ((Î± â†’áµ¤ Î²) Ã— (Î± â†’áµ¤ Î²)) :=
  (UniformFun.isBasis_gen Î± Î² ğ“•).filterBasis
#align uniform_fun.basis UniformFun.basis

/-- For `ğ“• : Filter (Î² Ã— Î²)`, this is the filter generated by the filter basis
`UniformFun.basis Î± Î² ğ“•`. For `ğ“• = ğ“¤ Î²`, this will be the uniformity of uniform
convergence on `Î±`. -/
protected def filter (ğ“• : Filter <| Î² Ã— Î²) : Filter ((Î± â†’áµ¤ Î²) Ã— (Î± â†’áµ¤ Î²)) :=
  (UniformFun.basis Î± Î² ğ“•).filter
#align uniform_fun.filter UniformFun.filter

-- mathport name: exprÎ¦
--local notation "Î¦" => fun (Î± Î² : Type*) (uvx : ((Î± â†’áµ¤ Î²) Ã— (Î± â†’áµ¤ Î²)) Ã— Î±) =>
  --(uvx.fst.fst uvx.2, uvx.1.2 uvx.2)

protected def phi (Î± Î² : Type*) (uvx : ((Î± â†’áµ¤ Î²) Ã— (Î± â†’áµ¤ Î²)) Ã— Î±) : Î² Ã— Î² :=
  (uvx.fst.fst uvx.2, uvx.1.2 uvx.2)

set_option quotPrecheck false -- Porting note: error message suggested to do this
/- This is a lower adjoint to `UniformFun.filter` (see `UniformFun.gc`).
The exact definition of the lower adjoint `l` is not interesting; we will only use that it exists
(in `UniformFun.mono` and `UniformFun.iInf_eq`) and that
`l (Filter.map (Prod.map f f) ğ“•) = Filter.map (Prod.map ((âˆ˜) f) ((âˆ˜) f)) (l ğ“•)` for each
`ğ“• : Filter (Î³ Ã— Î³)` and `f : Î³ â†’ Î±` (in `UniformFun.comap_eq`). -/
local notation "lower_adjoint" => fun ğ“ => map (UniformFun.phi Î± Î²) (ğ“ Ã—Ë¢ âŠ¤)

/-- The function `UniformFun.filter Î± Î² : Filter (Î² Ã— Î²) â†’ Filter ((Î± â†’áµ¤ Î²) Ã— (Î± â†’áµ¤ Î²))`
has a lower adjoint `l` (in the sense of `GaloisConnection`). The exact definition of `l` is not
interesting; we will only use that it exists (in `UniformFun.mono` and
`UniformFun.iInf_eq`) and that
`l (Filter.map (Prod.map f f) ğ“•) = Filter.map (Prod.map ((âˆ˜) f) ((âˆ˜) f)) (l ğ“•)` for each
`ğ“• : Filter (Î³ Ã— Î³)` and `f : Î³ â†’ Î±` (in `UniformFun.comap_eq`). -/
protected theorem gc : GaloisConnection lower_adjoint fun ğ“• => UniformFun.filter Î± Î² ğ“• := by
  intro ğ“ ğ“•
  symm
  calc
    ğ“ â‰¤ UniformFun.filter Î± Î² ğ“• â†” (UniformFun.basis Î± Î² ğ“•).sets âŠ† ğ“.sets :=
      by rw [UniformFun.filter, â† FilterBasis.generate, le_generate_iff]
    _ â†” âˆ€ U âˆˆ ğ“•, UniformFun.gen Î± Î² U âˆˆ ğ“ := image_subset_iff
    _ â†” âˆ€ U âˆˆ ğ“•,
          { uv | âˆ€ x, (uv, x) âˆˆ { t : ((Î± â†’áµ¤ Î²) Ã— (Î± â†’áµ¤ Î²)) Ã— Î± | (t.1.1 t.2, t.1.2 t.2) âˆˆ U } } âˆˆ
            ğ“ :=
      Iff.rfl
    _ â†” âˆ€ U âˆˆ ğ“•,
          { uvx : ((Î± â†’áµ¤ Î²) Ã— (Î± â†’áµ¤ Î²)) Ã— Î± | (uvx.1.1 uvx.2, uvx.1.2 uvx.2) âˆˆ U } âˆˆ
            ğ“ Ã—Ë¢ (âŠ¤ : Filter Î±) :=
      forallâ‚‚_congr fun U _hU => mem_prod_top.symm
    _ â†” lower_adjoint ğ“ â‰¤ ğ“• := Iff.rfl
#align uniform_fun.gc UniformFun.gc

variable [UniformSpace Î²]

/-- Core of the uniform structure of uniform convergence. -/
protected def uniformCore : UniformSpace.Core (Î± â†’áµ¤ Î²) :=
  UniformSpace.Core.mkOfBasis (UniformFun.basis Î± Î² (ğ“¤ Î²))
    (fun _ âŸ¨_, hV, hVUâŸ© _ => hVU â–¸ fun _ => refl_mem_uniformity hV)
    (fun _ âŸ¨V, hV, hVUâŸ© =>
      hVU â–¸
        âŸ¨UniformFun.gen Î± Î² (Prod.swap â»Â¹' V), âŸ¨Prod.swap â»Â¹' V, tendsto_swap_uniformity hV, rflâŸ©,
          fun _ huv x => huv xâŸ©)
    fun _ âŸ¨_, hV, hVUâŸ© =>
    hVU â–¸
      let âŸ¨W, hW, hWVâŸ© := comp_mem_uniformity_sets hV
      âŸ¨UniformFun.gen Î± Î² W, âŸ¨W, hW, rflâŸ©, fun _ âŸ¨w, huw, hwvâŸ© x => hWV âŸ¨w x, âŸ¨huw x, hwv xâŸ©âŸ©âŸ©
#align uniform_fun.uniform_core UniformFun.uniformCore

/-- Uniform structure of uniform convergence, declared as an instance on `Î± â†’áµ¤ Î²`.
We will denote it `ğ’°(Î±, Î², uÎ²)` in the rest of this file. -/
instance uniformSpace : UniformSpace (Î± â†’áµ¤ Î²) :=
  UniformSpace.ofCore (UniformFun.uniformCore Î± Î²)

/-- Topology of uniform convergence, declared as an instance on `Î± â†’áµ¤ Î²`. -/
instance topologicalSpace : TopologicalSpace (Î± â†’áµ¤ Î²) :=
  inferInstance

-- mathport name: Â«exprğ’°( , , )Â»
local notation "ğ’°(" Î± ", " Î² ", " u ")" => @UniformFun.uniformSpace Î± Î² u

/-- By definition, the uniformity of `Î± â†’áµ¤ Î²` admits the family `{(f, g) | âˆ€ x, (f x, g x) âˆˆ V}`
for `V âˆˆ ğ“¤ Î²` as a filter basis. -/
protected theorem hasBasis_uniformity :
    (ğ“¤ (Î± â†’áµ¤ Î²)).HasBasis (fun V => V âˆˆ ğ“¤ Î²) (UniformFun.gen Î± Î²) :=
  (UniformFun.isBasis_gen Î± Î² (ğ“¤ Î²)).hasBasis
#align uniform_fun.has_basis_uniformity UniformFun.hasBasis_uniformity

/-- The uniformity of `Î± â†’áµ¤ Î²` admits the family `{(f, g) | âˆ€ x, (f x, g x) âˆˆ V}` for `V âˆˆ ğ“‘` as
a filter basis, for any basis `ğ“‘` of `ğ“¤ Î²` (in the case `ğ“‘ = (ğ“¤ Î²).as_basis` this is true by
definition). -/
protected theorem hasBasis_uniformity_of_basis {Î¹ : Sort*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set (Î² Ã— Î²)}
    (h : (ğ“¤ Î²).HasBasis p s) : (ğ“¤ (Î± â†’áµ¤ Î²)).HasBasis p (UniformFun.gen Î± Î² âˆ˜ s) :=
  (UniformFun.hasBasis_uniformity Î± Î²).to_hasBasis
    (fun _ hU =>
      let âŸ¨i, hi, hiUâŸ© := h.mem_iff.mp hU
      âŸ¨i, hi, fun _ huv x => hiU (huv x)âŸ©)
    fun i hi => âŸ¨s i, h.mem_of_mem hi, subset_refl _âŸ©
#align uniform_fun.has_basis_uniformity_of_basis UniformFun.hasBasis_uniformity_of_basis

/-- For `f : Î± â†’áµ¤ Î²`, `ğ“ f` admits the family `{g | âˆ€ x, (f x, g x) âˆˆ V}` for `V âˆˆ ğ“‘` as a filter
basis, for any basis `ğ“‘` of `ğ“¤ Î²`. -/
protected theorem hasBasis_nhds_of_basis (f) {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set (Î² Ã— Î²)}
    (h : HasBasis (ğ“¤ Î²) p s) :
    (ğ“ f).HasBasis p fun i => { g | (f, g) âˆˆ UniformFun.gen Î± Î² (s i) } :=
  nhds_basis_uniformity' (UniformFun.hasBasis_uniformity_of_basis Î± Î² h)
#align uniform_fun.has_basis_nhds_of_basis UniformFun.hasBasis_nhds_of_basis

/-- For `f : Î± â†’áµ¤ Î²`, `ğ“ f` admits the family `{g | âˆ€ x, (f x, g x) âˆˆ V}` for `V âˆˆ ğ“¤ Î²` as a
filter basis. -/
protected theorem hasBasis_nhds (f) :
    (ğ“ f).HasBasis (fun V => V âˆˆ ğ“¤ Î²) fun V => { g | (f, g) âˆˆ UniformFun.gen Î± Î² V } :=
  UniformFun.hasBasis_nhds_of_basis Î± Î² f (Filter.basis_sets _)
#align uniform_fun.has_basis_nhds UniformFun.hasBasis_nhds

variable {Î±}

/-- Evaluation at a fixed point is uniformly continuous on `Î± â†’áµ¤ Î²`. -/
theorem uniformContinuous_eval (x : Î±) :
    UniformContinuous (Function.eval x âˆ˜ toFun : (Î± â†’áµ¤ Î²) â†’ Î²) := by
  change _ â‰¤ _
  rw [map_le_iff_le_comap,
    (UniformFun.hasBasis_uniformity Î± Î²).le_basis_iff ((ğ“¤ _).basis_sets.comap _)]
  exact fun U hU => âŸ¨U, hU, fun uv huv => huv xâŸ©
#align uniform_fun.uniform_continuous_eval UniformFun.uniformContinuous_eval

variable {Î²}

/-- If `uâ‚` and `uâ‚‚` are two uniform structures on `Î³` and `uâ‚ â‰¤ uâ‚‚`, then
`ğ’°(Î±, Î³, uâ‚) â‰¤ ğ’°(Î±, Î³, uâ‚‚)`. -/
protected theorem mono : Monotone (@UniformFun.uniformSpace Î± Î³) := fun _ _ hu =>
  (UniformFun.gc Î± Î³).monotone_u hu
#align uniform_fun.mono UniformFun.mono

/-- If `u` is a family of uniform structures on `Î³`, then
`ğ’°(Î±, Î³, (â¨… i, u i)) = â¨… i, ğ’°(Î±, Î³, u i)`. -/
protected theorem iInf_eq {u : Î¹ â†’ UniformSpace Î³} : ğ’°(Î±, Î³, (â¨… i, u i)) = â¨… i, ğ’°(Î±, Î³, u i) := by
  -- This follows directly from the fact that the upper adjoint in a Galois connection maps
  -- infimas to infimas.
  ext : 1
  change UniformFun.filter Î± Î³ ğ“¤[â¨… i, u i] = ğ“¤[â¨… i, ğ’°(Î±, Î³, u i)]
  rw [iInf_uniformity, iInf_uniformity]
  exact (UniformFun.gc Î± Î³).u_iInf
#align uniform_fun.infi_eq UniformFun.iInf_eq

/-- If `uâ‚` and `uâ‚‚` are two uniform structures on `Î³`, then
`ğ’°(Î±, Î³, uâ‚ âŠ“ uâ‚‚) = ğ’°(Î±, Î³, uâ‚) âŠ“ ğ’°(Î±, Î³, uâ‚‚)`. -/
protected theorem inf_eq {uâ‚ uâ‚‚ : UniformSpace Î³} :
    ğ’°(Î±, Î³, uâ‚ âŠ“ uâ‚‚) = ğ’°(Î±, Î³, uâ‚) âŠ“ ğ’°(Î±, Î³, uâ‚‚) := by
  -- This follows directly from the fact that the upper adjoint in a Galois connection maps
  -- infimas to infimas.
  rw [inf_eq_iInf, inf_eq_iInf, UniformFun.iInf_eq]
  refine' iInf_congr fun i => _
  cases i <;> rfl
#align uniform_fun.inf_eq UniformFun.inf_eq

-- porting note: had to add a type annotation at `((f âˆ˜ Â·) : ((Î± â†’ Î³) â†’ (Î± â†’ Î²)))`
/-- If `u` is a uniform structures on `Î²` and `f : Î³ â†’ Î²`, then
`ğ’°(Î±, Î³, comap f u) = comap (fun g â†¦ f âˆ˜ g) ğ’°(Î±, Î³, uâ‚)`. -/
protected theorem comap_eq {f : Î³ â†’ Î²} :
    ğ’°(Î±, Î³, â€¹UniformSpace Î²â€º.comap f) = ğ’°(Î±, Î², _).comap ((f âˆ˜ Â·)) := by
  letI : UniformSpace Î³ := â€¹UniformSpace Î²â€º.comap f
  ext : 1
  change UniformFun.filter Î± Î³ ((ğ“¤ Î²).comap _) = (UniformFun.filter Î± Î² (ğ“¤ Î²)).comap _
  -- We have the following four Galois connection which form a square diagram, and we want
  -- to show that the square of upper adjoints is commutative. The trick then is to use
  -- `GaloisConnection.u_comm_of_l_comm` to reduce it to commutativity of the lower adjoints,
  -- which is way easier to prove.
  let fcomp := ((f âˆ˜ Â·) : ((Î± â†’ Î³) â†’ (Î± â†’ Î²)))
  have hâ‚ := Filter.gc_map_comap (Prod.map fcomp fcomp)
  have hâ‚‚ := Filter.gc_map_comap (Prod.map f f)
  have hâ‚ƒ := UniformFun.gc Î± Î²
  have hâ‚„ := UniformFun.gc Î± Î³
  refine' GaloisConnection.u_comm_of_l_comm hâ‚ hâ‚‚ hâ‚ƒ hâ‚„ fun ğ“ => _
  have : Prod.map f f âˆ˜ (UniformFun.phi Î± Î³)
      = (UniformFun.phi Î± Î²) âˆ˜ Prod.map (Prod.map fcomp fcomp) (id : Î± â†’ Î±) := by
    ext <;> rfl
  rw [map_comm this, â† prod_map_map_eq']
  rfl
#align uniform_fun.comap_eq UniformFun.comap_eq

/-- Post-composition by a uniformly continuous function is uniformly continuous on `Î± â†’áµ¤ Î²`.

More precisely, if `f : Î³ â†’ Î²` is uniformly continuous, then `(fun g â†¦ f âˆ˜ g) : (Î± â†’áµ¤ Î³) â†’ (Î± â†’áµ¤ Î²)`
is uniformly continuous. -/
protected theorem postcomp_uniformContinuous [UniformSpace Î³] {f : Î³ â†’ Î²}
    (hf : UniformContinuous f) :
    UniformContinuous (ofFun âˆ˜ (f âˆ˜ Â·) âˆ˜ toFun : (Î± â†’áµ¤ Î³) â†’ Î± â†’áµ¤ Î²) := by
  -- This is a direct consequence of `UniformFun.comap_eq`
    refine uniformContinuous_iff.mpr ?_
    exact (UniformFun.mono (uniformContinuous_iff.mp hf)).trans_eq UniformFun.comap_eq
    -- porting note: the original calc proof below gives a deterministic timeout
    --calc
    --  ğ’°(Î±, Î³, _) â‰¤ ğ’°(Î±, Î³, â€¹UniformSpace Î²â€º.comap f) :=
    --    UniformFun.mono (uniformContinuous_iff.mp hf)
    --  _ = ğ’°(Î±, Î², _).comap (f âˆ˜ Â·) := @UniformFun.comap_eq Î± Î² Î³ _ f
#align uniform_fun.postcomp_uniform_continuous UniformFun.postcomp_uniformContinuous

/-- Post-composition by a uniform inducing is a uniform inducing for the
uniform structures of uniform convergence.

More precisely, if `f : Î³ â†’ Î²` is a uniform inducing, then `(Î» g, f âˆ˜ g) : (Î± â†’áµ¤ Î³) â†’ (Î± â†’áµ¤ Î²)` is
a uniform inducing. -/
protected theorem postcomp_uniformInducing [UniformSpace Î³] {f : Î³ â†’ Î²} (hf : UniformInducing f) :
    UniformInducing (ofFun âˆ˜ (f âˆ˜ Â·) âˆ˜ toFun : (Î± â†’áµ¤ Î³) â†’ Î± â†’áµ¤ Î²) := by
  -- This is a direct consequence of `UniformFun.comap_eq`
  constructor
  replace hf : (ğ“¤ Î²).comap (Prod.map f f) = _ := hf.comap_uniformity
  change comap (Prod.map (ofFun âˆ˜ (f âˆ˜ Â·) âˆ˜ toFun) (ofFun âˆ˜ (f âˆ˜ Â·) âˆ˜ toFun)) _ = _
  rw [â† uniformity_comap] at hf âŠ¢
  congr
  rw [â† uniformSpace_eq hf, UniformFun.comap_eq]
  rfl
#align uniform_fun.postcomp_uniform_inducing UniformFun.postcomp_uniformInducing

/-- Turn a uniform isomorphism `Î³ â‰ƒáµ¤ Î²` into a uniform isomorphism `(Î± â†’áµ¤ Î³) â‰ƒáµ¤ (Î± â†’áµ¤ Î²)` by
post-composing. -/
protected def congrRight [UniformSpace Î³] (e : Î³ â‰ƒáµ¤ Î²) : (Î± â†’áµ¤ Î³) â‰ƒáµ¤ (Î± â†’áµ¤ Î²) :=
  { Equiv.piCongrRight fun _a =>
      e.toEquiv with
    uniformContinuous_toFun := UniformFun.postcomp_uniformContinuous e.uniformContinuous
    uniformContinuous_invFun := UniformFun.postcomp_uniformContinuous e.symm.uniformContinuous }
#align uniform_fun.congr_right UniformFun.congrRight

/-- Pre-composition by any function is uniformly continuous for the uniform structures of
uniform convergence.

More precisely, for any `f : Î³ â†’ Î±`, the function `(Î» g, g âˆ˜ f) : (Î± â†’áµ¤ Î²) â†’ (Î³ â†’áµ¤ Î²)` is uniformly
continuous. -/
protected theorem precomp_uniformContinuous {f : Î³ â†’ Î±} :
    UniformContinuous fun g : Î± â†’áµ¤ Î² => ofFun (g âˆ˜ f) := by
  -- Here we simply go back to filter bases.
  rw [uniformContinuous_iff]
  change
    ğ“¤ (Î± â†’áµ¤ Î²) â‰¤ (ğ“¤ (Î³ â†’áµ¤ Î²)).comap (Prod.map (fun g : Î± â†’áµ¤ Î² => g âˆ˜ f) fun g : Î± â†’áµ¤ Î² => g âˆ˜ f)
  rw [(UniformFun.hasBasis_uniformity Î± Î²).le_basis_iff
      ((UniformFun.hasBasis_uniformity Î³ Î²).comap _)]
  exact fun U hU => âŸ¨U, hU, fun uv huv x => huv (f x)âŸ©
#align uniform_fun.precomp_uniform_continuous UniformFun.precomp_uniformContinuous

/-- Turn a bijection `Î³ â‰ƒ Î±` into a uniform isomorphism
`(Î³ â†’áµ¤ Î²) â‰ƒáµ¤ (Î± â†’áµ¤ Î²)` by pre-composing. -/
protected def congrLeft (e : Î³ â‰ƒ Î±) : (Î³ â†’áµ¤ Î²) â‰ƒáµ¤ (Î± â†’áµ¤ Î²) :=
  { Equiv.arrowCongr e
      (Equiv.refl
        _) with
    uniformContinuous_toFun := UniformFun.precomp_uniformContinuous
    uniformContinuous_invFun := UniformFun.precomp_uniformContinuous }
#align uniform_fun.congr_left UniformFun.congrLeft

/-- The topology of uniform convergence is Tâ‚‚. -/
instance [T2Space Î²] : T2Space (Î± â†’áµ¤ Î²) where
  t2 := by
    intro f g h
    obtain âŸ¨x, hxâŸ© := not_forall.mp (mt funext h)
    exact separated_by_continuous (uniformContinuous_eval Î² x).continuous hx

/-- The natural map `UniformFun.toFun` from `Î± â†’áµ¤ Î²` to `Î± â†’ Î²` is uniformly continuous.

In other words, the uniform structure of uniform convergence is finer than that of pointwise
convergence, aka the product uniform structure. -/
protected theorem uniformContinuous_toFun : UniformContinuous (toFun : (Î± â†’áµ¤ Î²) â†’ Î± â†’ Î²) := by
  -- By definition of the product uniform structure, this is just `uniform_continuous_eval`.
  rw [uniformContinuous_pi]
  intro x
  exact uniformContinuous_eval Î² x
#align uniform_fun.uniform_continuous_to_fun UniformFun.uniformContinuous_toFun

/-- The topology of uniform convergence indeed gives the same notion of convergence as
`TendstoUniformly`. -/
protected theorem tendsto_iff_tendstoUniformly {F : Î¹ â†’ Î± â†’áµ¤ Î²} {f : Î± â†’áµ¤ Î²} :
    Tendsto F p (ğ“ f) â†” TendstoUniformly F f p := by
  rw [(UniformFun.hasBasis_nhds Î± Î² f).tendsto_right_iff, TendstoUniformly]
  exact Iff.rfl
#align uniform_fun.tendsto_iff_tendsto_uniformly UniformFun.tendsto_iff_tendstoUniformly

/-- The natural bijection between `Î± â†’ Î² Ã— Î³` and `(Î± â†’ Î²) Ã— (Î± â†’ Î³)`, upgraded to a uniform
isomorphism between `Î± â†’áµ¤ Î² Ã— Î³` and `(Î± â†’áµ¤ Î²) Ã— (Î± â†’áµ¤ Î³)`. -/
protected def uniformEquivProdArrow [UniformSpace Î³] : (Î± â†’áµ¤ Î² Ã— Î³) â‰ƒáµ¤ (Î± â†’áµ¤ Î²) Ã— (Î± â†’áµ¤ Î³) :=
  -- Denote `Ï†` this bijection. We want to show that
  -- `comap Ï† (ğ’°(Î±, Î², uÎ²) Ã— ğ’°(Î±, Î³, uÎ³)) = ğ’°(Î±, Î² Ã— Î³, uÎ² Ã— uÎ³)`.
  -- But `uÎ² Ã— uÎ³` is defined as `comap fst uÎ² âŠ“ comap snd uÎ³`, so we just have to apply
  -- `UniformFun.inf_eq` and `UniformFun.comap_eq`, which leaves us to check
  -- that some square commutes.
  Equiv.toUniformEquivOfUniformInducing (Equiv.arrowProdEquivProdArrow _ _ _) $ by
    constructor
    change
      comap (Prod.map (Equiv.arrowProdEquivProdArrow _ _ _) (Equiv.arrowProdEquivProdArrow _ _ _))
          _ = _
    simp_rw [UniformFun]
    rw [â† uniformity_comap]
    congr
    unfold instUniformSpaceProd
    rw [UniformSpace.comap_inf, â† UniformSpace.comap_comap, â† UniformSpace.comap_comap]
    have := (@UniformFun.inf_eq Î± (Î² Ã— Î³)
      (UniformSpace.comap Prod.fst â€¹_â€º) (UniformSpace.comap Prod.snd â€¹_â€º)).symm
    rwa [UniformFun.comap_eq, UniformFun.comap_eq] at this
#align uniform_fun.uniform_equiv_prod_arrow UniformFun.uniformEquivProdArrow

-- the relevant diagram commutes by definition
variable (Î±) (Î´ : Î¹ â†’ Type*) [âˆ€ i, UniformSpace (Î´ i)]

/-- The natural bijection between `Î± â†’ Î  i, Î´ i` and `Î  i, Î± â†’ Î´ i`, upgraded to a uniform
isomorphism between `Î± â†’áµ¤ (Î  i, Î´ i)` and `Î  i, Î± â†’áµ¤ Î´ i`. -/
protected def uniformEquivPiComm : UniformEquiv (Î± â†’áµ¤ âˆ€ i, Î´ i) (âˆ€ i, Î± â†’áµ¤ Î´ i) :=
  -- Denote `Ï†` this bijection. We want to show that
    -- `comap Ï† (Î  i, ğ’°(Î±, Î´ i, uÎ´ i)) = ğ’°(Î±, (Î  i, Î´ i), (Î  i, uÎ´ i))`.
    -- But `Î  i, uÎ´ i` is defined as `â¨… i, comap (eval i) (uÎ´ i)`, so we just have to apply
    -- `UniformFun.iInf_eq` and `UniformFun.comap_eq`, which leaves us to check
    -- that some square commutes.
    @Equiv.toUniformEquivOfUniformInducing
    _ _ ğ’°(Î±, âˆ€ i, Î´ i, Pi.uniformSpace Î´)
    (@Pi.uniformSpace Î¹ (fun i => Î± â†’ Î´ i) fun i => ğ’°(Î±, Î´ i, _)) (Equiv.piComm _) $ by
      refine @UniformInducing.mk ?_ ?_ ?_ ?_ ?_ ?_
      change comap (Prod.map Function.swap Function.swap) _ = _
      rw [â† uniformity_comap]
      congr
      unfold Pi.uniformSpace
      rw [UniformSpace.ofCoreEq_toCore, UniformSpace.ofCoreEq_toCore,
        UniformSpace.comap_iInf, UniformFun.iInf_eq]
      refine' iInf_congr fun i => _
      rw [â† UniformSpace.comap_comap, UniformFun.comap_eq]
      rfl
#align uniform_fun.uniform_equiv_Pi_comm UniformFun.uniformEquivPiComm

-- Like in the previous lemma, the diagram actually commutes by definition
end UniformFun

namespace UniformOnFun

variable {Î± Î² : Type*} {Î³ Î¹ : Type*}

variable {s s' : Set Î±} {x : Î±} {p : Filter Î¹} {g : Î¹ â†’ Î±}

-- mathport name: Â«exprğ’°( , , )Â»
local notation "ğ’°(" Î± ", " Î² ", " u ")" => @UniformFun.uniformSpace Î± Î² u

/-- Basis sets for the uniformity of `ğ”–`-convergence: for `S : Set Î±` and `V : Set (Î² Ã— Î²)`,
`gen ğ”– S V` is the set of pairs `(f, g)` of functions `Î± â†’áµ¤[ğ”–] Î²` such that
`âˆ€ x âˆˆ S, (f x, g x) âˆˆ V`. Note that the family `ğ”– : Set (Set Î±)` is only used to specify which
type alias of `Î± â†’ Î²` to use here. -/
protected def gen (ğ”–) (S : Set Î±) (V : Set (Î² Ã— Î²)) : Set ((Î± â†’áµ¤[ğ”–] Î²) Ã— (Î± â†’áµ¤[ğ”–] Î²)) :=
  { uv : (Î± â†’áµ¤[ğ”–] Î²) Ã— (Î± â†’áµ¤[ğ”–] Î²) | âˆ€ x âˆˆ S, (uv.1 x, uv.2 x) âˆˆ V }
#align uniform_on_fun.gen UniformOnFun.gen

/-- For `S : Set Î±` and `V : Set (Î² Ã— Î²)`, we have
`UniformOnFun.gen ğ”– S V = (S.restrict Ã— S.restrict) â»Â¹' (UniformFun.gen S Î² V)`.
This is the crucial fact for proving that the family `UniformOnFun.gen S V` for `S âˆˆ ğ”–` and
`V âˆˆ ğ“¤ Î²` is indeed a basis for the uniformity `Î± â†’áµ¤[ğ”–] Î²` endowed with `ğ’±(Î±, Î², ğ”–, uÎ²)`
the uniform structure of `ğ”–`-convergence, as defined in `uniform_on_fun.uniform_space`. -/
protected theorem gen_eq_preimage_restrict {ğ”–} (S : Set Î±) (V : Set (Î² Ã— Î²)) :
    UniformOnFun.gen ğ”– S V =
      Prod.map (S.restrict âˆ˜ UniformFun.toFun) (S.restrict âˆ˜ UniformFun.toFun) â»Â¹'
        UniformFun.gen S Î² V := by
  ext uv
  exact âŸ¨fun h âŸ¨x, hxâŸ© => h x hx, fun h x hx => h âŸ¨x, hxâŸ©âŸ©
#align uniform_on_fun.gen_eq_preimage_restrict UniformOnFun.gen_eq_preimage_restrict

/-- `UniformOnFun.gen` is antitone in the first argument and monotone in the second. -/
protected theorem gen_mono {ğ”–} {S S' : Set Î±} {V V' : Set (Î² Ã— Î²)} (hS : S' âŠ† S) (hV : V âŠ† V') :
    UniformOnFun.gen ğ”– S V âŠ† UniformOnFun.gen ğ”– S' V' := fun _uv h x hx => hV (h x <| hS hx)
#align uniform_on_fun.gen_mono UniformOnFun.gen_mono

/-- If `ğ”– : Set (Set Î±)` is nonempty and directed and `ğ“‘` is a filter basis on `Î² Ã— Î²`, then the
family `UniformOnFun.gen ğ”– S V` for `S âˆˆ ğ”–` and `V âˆˆ ğ“‘` is a filter basis on
`(Î± â†’áµ¤[ğ”–] Î²) Ã— (Î± â†’áµ¤[ğ”–] Î²)`.
We will show in `has_basis_uniformity_of_basis` that, if `ğ“‘` is a basis for `ğ“¤ Î²`, then the
corresponding filter is the uniformity of `Î± â†’áµ¤[ğ”–] Î²`. -/
protected theorem isBasis_gen (ğ”– : Set (Set Î±)) (h : ğ”–.Nonempty) (h' : DirectedOn (Â· âŠ† Â·) ğ”–)
    (ğ“‘ : FilterBasis <| Î² Ã— Î²) :
    IsBasis (fun SV : Set Î± Ã— Set (Î² Ã— Î²) => SV.1 âˆˆ ğ”– âˆ§ SV.2 âˆˆ ğ“‘) fun SV =>
      UniformOnFun.gen ğ”– SV.1 SV.2 :=
  âŸ¨h.prod ğ“‘.nonempty, fun {Uâ‚Vâ‚ Uâ‚‚Vâ‚‚} hâ‚ hâ‚‚ =>
    let âŸ¨Uâ‚ƒ, hUâ‚ƒ, hUâ‚â‚ƒ, hUâ‚‚â‚ƒâŸ© := h' Uâ‚Vâ‚.1 hâ‚.1 Uâ‚‚Vâ‚‚.1 hâ‚‚.1
    let âŸ¨Vâ‚ƒ, hVâ‚ƒ, hVâ‚â‚‚â‚ƒâŸ© := ğ“‘.inter_sets hâ‚.2 hâ‚‚.2
    âŸ¨âŸ¨Uâ‚ƒ, Vâ‚ƒâŸ©,
      âŸ¨âŸ¨hUâ‚ƒ, hVâ‚ƒâŸ©, fun _ H =>
        âŸ¨fun x hx => (hVâ‚â‚‚â‚ƒ <| H x <| hUâ‚â‚ƒ hx).1, fun x hx => (hVâ‚â‚‚â‚ƒ <| H x <| hUâ‚‚â‚ƒ hx).2âŸ©âŸ©âŸ©âŸ©
#align uniform_on_fun.is_basis_gen UniformOnFun.isBasis_gen

variable (Î± Î²) [UniformSpace Î²] (ğ”– : Set (Set Î±))

/-- Uniform structure of `ğ”–`-convergence, i.e uniform convergence on the elements of `ğ”–`,
declared as an instance on `Î± â†’áµ¤[ğ”–] Î²`. It is defined as the infimum, for `S âˆˆ ğ”–`, of the pullback
by `S.restrict`, the map of restriction to `S`, of the uniform structure `ğ’°(s, Î², uÎ²)` on
`â†¥S â†’áµ¤ Î²`. We will denote it `ğ’±(Î±, Î², ğ”–, uÎ²)`, where `uÎ²` is the uniform structure on `Î²`. -/
instance uniformSpace : UniformSpace (Î± â†’áµ¤[ğ”–] Î²) :=
  â¨… (s : Set Î±) (_ : s âˆˆ ğ”–), UniformSpace.comap s.restrict ğ’°(s, Î², _)

local notation "ğ’±(" Î± ", " Î² ", " ğ”– ", " u ")" => @UniformOnFun.uniformSpace Î± Î² u ğ”–

/-- Topology of `ğ”–`-convergence, i.e uniform convergence on the elements of `ğ”–`, declared as an
instance on `Î± â†’áµ¤[ğ”–] Î²`. -/
instance topologicalSpace : TopologicalSpace (Î± â†’áµ¤[ğ”–] Î²) :=
  ğ’±(Î±, Î², ğ”–, _).toTopologicalSpace

/-- The topology of `ğ”–`-convergence is the infimum, for `S âˆˆ ğ”–`, of topology induced by the map
of `S.restrict : (Î± â†’áµ¤[ğ”–] Î²) â†’ (â†¥S â†’áµ¤ Î²)` of restriction to `S`, where `â†¥S â†’áµ¤ Î²` is endowed with
the topology of uniform convergence. -/
protected theorem topologicalSpace_eq :
    UniformOnFun.topologicalSpace Î± Î² ğ”– =
      â¨… (s : Set Î±) (_ : s âˆˆ ğ”–), TopologicalSpace.induced (s.restrict âˆ˜ UniformFun.toFun)
        (UniformFun.topologicalSpace s Î²) := by
  simp only [UniformOnFun.topologicalSpace, toTopologicalSpace_iInf, toTopologicalSpace_iInf,
    toTopologicalSpace_comap]
  rfl
#align uniform_on_fun.topological_space_eq UniformOnFun.topologicalSpace_eq

protected theorem hasBasis_uniformity_of_basis_auxâ‚ {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set (Î² Ã— Î²)}
    (hb : HasBasis (ğ“¤ Î²) p s) (S : Set Î±) :
    (@uniformity (Î± â†’áµ¤[ğ”–] Î²) ((UniformFun.uniformSpace S Î²).comap S.restrict)).HasBasis p fun i =>
      UniformOnFun.gen ğ”– S (s i) := by
  simp_rw [UniformOnFun.gen_eq_preimage_restrict, uniformity_comap]
  exact (UniformFun.hasBasis_uniformity_of_basis S Î² hb).comap _
#align uniform_on_fun.has_basis_uniformity_of_basis_auxâ‚ UniformOnFun.hasBasis_uniformity_of_basis_auxâ‚

protected theorem hasBasis_uniformity_of_basis_auxâ‚‚ (h : DirectedOn (Â· âŠ† Â·) ğ”–) {p : Î¹ â†’ Prop}
    {s : Î¹ â†’ Set (Î² Ã— Î²)} (hb : HasBasis (ğ“¤ Î²) p s) :
    DirectedOn
      ((fun s : Set Î± => (UniformFun.uniformSpace s Î²).comap (s.restrict : (Î± â†’áµ¤ Î²) â†’ s â†’áµ¤ Î²)) â»Â¹'o
        GE.ge)
      ğ”– :=
  h.mono fun hst =>
    ((UniformOnFun.hasBasis_uniformity_of_basis_auxâ‚ Î± Î² ğ”– hb _).le_basis_iff
          (UniformOnFun.hasBasis_uniformity_of_basis_auxâ‚ Î± Î² ğ”– hb _)).mpr
      fun V hV => âŸ¨V, hV, UniformOnFun.gen_mono hst subset_rflâŸ©
#align uniform_on_fun.has_basis_uniformity_of_basis_auxâ‚‚ UniformOnFun.hasBasis_uniformity_of_basis_auxâ‚‚

/-- If `ğ”– : Set (Set Î±)` is nonempty and directed and `ğ“‘` is a filter basis of `ğ“¤ Î²`, then the
uniformity of `Î± â†’áµ¤[ğ”–] Î²` admits the family `{(f, g) | âˆ€ x âˆˆ S, (f x, g x) âˆˆ V}` for `S âˆˆ ğ”–` and
`V âˆˆ ğ“‘` as a filter basis. -/
protected theorem hasBasis_uniformity_of_basis (h : ğ”–.Nonempty) (h' : DirectedOn (Â· âŠ† Â·) ğ”–)
    {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set (Î² Ã— Î²)} (hb : HasBasis (ğ“¤ Î²) p s) :
    (ğ“¤ (Î± â†’áµ¤[ğ”–] Î²)).HasBasis (fun Si : Set Î± Ã— Î¹ => Si.1 âˆˆ ğ”– âˆ§ p Si.2) fun Si =>
      UniformOnFun.gen ğ”– Si.1 (s Si.2) := by
  simp only [iInf_uniformity]
  exact
    hasBasis_biInf_of_directed h (fun S => UniformOnFun.gen ğ”– S âˆ˜ s) _
      (fun S _hS => UniformOnFun.hasBasis_uniformity_of_basis_auxâ‚ Î± Î² ğ”– hb S)
      (UniformOnFun.hasBasis_uniformity_of_basis_auxâ‚‚ Î± Î² ğ”– h' hb)
#align uniform_on_fun.has_basis_uniformity_of_basis UniformOnFun.hasBasis_uniformity_of_basis

/-- If `ğ”– : Set (Set Î±)` is nonempty and directed, then the uniformity of `Î± â†’áµ¤[ğ”–] Î²` admits the
family `{(f, g) | âˆ€ x âˆˆ S, (f x, g x) âˆˆ V}` for `S âˆˆ ğ”–` and `V âˆˆ ğ“¤ Î²` as a filter basis. -/
protected theorem hasBasis_uniformity (h : ğ”–.Nonempty) (h' : DirectedOn (Â· âŠ† Â·) ğ”–) :
    (ğ“¤ (Î± â†’áµ¤[ğ”–] Î²)).HasBasis (fun SV : Set Î± Ã— Set (Î² Ã— Î²) => SV.1 âˆˆ ğ”– âˆ§ SV.2 âˆˆ ğ“¤ Î²) fun SV =>
      UniformOnFun.gen ğ”– SV.1 SV.2 :=
  UniformOnFun.hasBasis_uniformity_of_basis Î± Î² ğ”– h h' (ğ“¤ Î²).basis_sets
#align uniform_on_fun.has_basis_uniformity UniformOnFun.hasBasis_uniformity

/-- For `f : Î± â†’áµ¤[ğ”–] Î²`, where `ğ”– : Set (Set Î±)` is nonempty and directed, `ğ“ f` admits the
family `{g | âˆ€ x âˆˆ S, (f x, g x) âˆˆ V}` for `S âˆˆ ğ”–` and `V âˆˆ ğ“‘` as a filter basis, for any basis
`ğ“‘` of `ğ“¤ Î²`. -/
protected theorem hasBasis_nhds_of_basis (f : Î± â†’áµ¤[ğ”–] Î²) (h : ğ”–.Nonempty)
    (h' : DirectedOn (Â· âŠ† Â·) ğ”–) {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set (Î² Ã— Î²)} (hb : HasBasis (ğ“¤ Î²) p s) :
    (ğ“ f).HasBasis (fun Si : Set Î± Ã— Î¹ => Si.1 âˆˆ ğ”– âˆ§ p Si.2) fun Si =>
      { g | (g, f) âˆˆ UniformOnFun.gen ğ”– Si.1 (s Si.2) } :=
  letI : UniformSpace (Î± â†’ Î²) := UniformOnFun.uniformSpace Î± Î² ğ”–
  nhds_basis_uniformity (UniformOnFun.hasBasis_uniformity_of_basis Î± Î² ğ”– h h' hb)
#align uniform_on_fun.has_basis_nhds_of_basis UniformOnFun.hasBasis_nhds_of_basis

/-- For `f : Î± â†’áµ¤[ğ”–] Î²`, where `ğ”– : Set (Set Î±)` is nonempty and directed, `ğ“ f` admits the
family `{g | âˆ€ x âˆˆ S, (f x, g x) âˆˆ V}` for `S âˆˆ ğ”–` and `V âˆˆ ğ“¤ Î²` as a filter basis. -/
protected theorem hasBasis_nhds (f : Î± â†’áµ¤[ğ”–] Î²) (h : ğ”–.Nonempty) (h' : DirectedOn (Â· âŠ† Â·) ğ”–) :
    (ğ“ f).HasBasis (fun SV : Set Î± Ã— Set (Î² Ã— Î²) => SV.1 âˆˆ ğ”– âˆ§ SV.2 âˆˆ ğ“¤ Î²) fun SV =>
      { g | (g, f) âˆˆ UniformOnFun.gen ğ”– SV.1 SV.2 } :=
  UniformOnFun.hasBasis_nhds_of_basis Î± Î² ğ”– f h h' (Filter.basis_sets _)
#align uniform_on_fun.has_basis_nhds UniformOnFun.hasBasis_nhds

/-- If `S âˆˆ ğ”–`, then the restriction to `S` is a uniformly continuous map from `Î± â†’áµ¤[ğ”–] Î²` to
`â†¥S â†’áµ¤ Î²`. -/
protected theorem uniformContinuous_restrict (h : s âˆˆ ğ”–) :
    UniformContinuous (UniformFun.ofFun âˆ˜ (s.restrict : (Î± â†’ Î²) â†’ s â†’ Î²) âˆ˜ toFun ğ”–) := by
  change _ â‰¤ _
  simp only [UniformOnFun.uniformSpace, map_le_iff_le_comap, iInf_uniformity]
  exact iInfâ‚‚_le s h
#align uniform_on_fun.uniform_continuous_restrict UniformOnFun.uniformContinuous_restrict

variable {Î±}

/-- Let `uâ‚`, `uâ‚‚` be two uniform structures on `Î³` and `ğ”–â‚ ğ”–â‚‚ : Set (Set Î±)`. If `uâ‚ â‰¤ uâ‚‚` and
`ğ”–â‚‚ âŠ† ğ”–â‚` then `ğ’±(Î±, Î³, ğ”–â‚, uâ‚) â‰¤ ğ’±(Î±, Î³, ğ”–â‚‚, uâ‚‚)`. -/
protected theorem mono â¦ƒuâ‚ uâ‚‚ : UniformSpace Î³â¦„ (hu : uâ‚ â‰¤ uâ‚‚) â¦ƒğ”–â‚ ğ”–â‚‚ : Set (Set Î±)â¦„
    (hğ”– : ğ”–â‚‚ âŠ† ğ”–â‚) : ğ’±(Î±, Î³, ğ”–â‚, uâ‚) â‰¤ ğ’±(Î±, Î³, ğ”–â‚‚, uâ‚‚) :=
  calc
    ğ’±(Î±, Î³, ğ”–â‚, uâ‚) â‰¤ ğ’±(Î±, Î³, ğ”–â‚‚, uâ‚) := iInf_le_iInf_of_subset hğ”–
    _ â‰¤ ğ’±(Î±, Î³, ğ”–â‚‚, uâ‚‚) := iInfâ‚‚_mono fun _i _hi => UniformSpace.comap_mono <| UniformFun.mono hu
#align uniform_on_fun.mono UniformOnFun.mono

/-- If `x : Î±` is in some `S âˆˆ ğ”–`, then evaluation at `x` is uniformly continuous on
`Î± â†’áµ¤[ğ”–] Î²`. -/
theorem uniformContinuous_eval_of_mem {x : Î±} (hxs : x âˆˆ s) (hs : s âˆˆ ğ”–) :
    UniformContinuous ((Function.eval x : (Î± â†’ Î²) â†’ Î²) âˆ˜ toFun ğ”–) :=
  (UniformFun.uniformContinuous_eval Î² (âŸ¨x, hxsâŸ© : s)).comp
    (UniformOnFun.uniformContinuous_restrict Î± Î² ğ”– hs)
#align uniform_on_fun.uniform_continuous_eval_of_mem UniformOnFun.uniformContinuous_eval_of_mem

variable {Î²} {ğ”–}

/-- If `u` is a family of uniform structures on `Î³`, then
`ğ’±(Î±, Î³, ğ”–, (â¨… i, u i)) = â¨… i, ğ’±(Î±, Î³, ğ”–, u i)`. -/
protected theorem iInf_eq {u : Î¹ â†’ UniformSpace Î³} :
    ğ’±(Î±, Î³, ğ”–, â¨… i, u i) = â¨… i, ğ’±(Î±, Î³, ğ”–, u i) := by
  simp_rw [UniformOnFun.uniformSpace, UniformFun.iInf_eq, UniformSpace.comap_iInf]
  rw [iInf_comm]
  exact iInf_congr fun s => iInf_comm
#align uniform_on_fun.infi_eq UniformOnFun.iInf_eq

/-- If `uâ‚` and `uâ‚‚` are two uniform structures on `Î³`, then
`ğ’±(Î±, Î³, ğ”–, uâ‚ âŠ“ uâ‚‚) = ğ’±(Î±, Î³, ğ”–, uâ‚) âŠ“ ğ’±(Î±, Î³, ğ”–, uâ‚‚)`. -/
protected theorem inf_eq {uâ‚ uâ‚‚ : UniformSpace Î³} :
    ğ’±(Î±, Î³, ğ”–, uâ‚ âŠ“ uâ‚‚) = ğ’±(Î±, Î³, ğ”–, uâ‚) âŠ“ ğ’±(Î±, Î³, ğ”–, uâ‚‚) := by
  rw [inf_eq_iInf, inf_eq_iInf, UniformOnFun.iInf_eq]
  refine' iInf_congr fun i => _
  cases i <;> rfl
#align uniform_on_fun.inf_eq UniformOnFun.inf_eq

/-- If `u` is a uniform structure on `Î²` and `f : Î³ â†’ Î²`, then
`ğ’±(Î±, Î³, ğ”–, comap f u) = comap (fun g â†¦ f âˆ˜ g) ğ’±(Î±, Î³, ğ”–, uâ‚)`. -/
protected theorem comap_eq {f : Î³ â†’ Î²} :
    ğ’±(Î±, Î³, ğ”–, â€¹UniformSpace Î²â€º.comap f) = ğ’±(Î±, Î², ğ”–, _).comap ((Â· âˆ˜ Â·) f) := by
  -- We reduce this to `UniformFun.comap_eq` using the fact that `comap` distributes
  -- on `iInf`.
  simp_rw [UniformOnFun.uniformSpace, UniformSpace.comap_iInf, UniformFun.comap_eq, â†
    UniformSpace.comap_comap]
  rfl
#align uniform_on_fun.comap_eq UniformOnFun.comap_eq

-- by definition, `âˆ€ S âˆˆ ğ”–, (f âˆ˜ â€”) âˆ˜ S.restrict = S.restrict âˆ˜ (f âˆ˜ â€”)`.
/-- Post-composition by a uniformly continuous function is uniformly continuous for the
uniform structures of `ğ”–`-convergence.

More precisely, if `f : Î³ â†’ Î²` is uniformly continuous, then
`(fun g â†¦ f âˆ˜ g) : (Î± â†’áµ¤[ğ”–] Î³) â†’ (Î± â†’áµ¤[ğ”–] Î²)` is uniformly continuous. -/
protected theorem postcomp_uniformContinuous [UniformSpace Î³] {f : Î³ â†’ Î²}
    (hf : UniformContinuous f) : UniformContinuous (ofFun ğ”– âˆ˜ (Â· âˆ˜ Â·) f âˆ˜ toFun ğ”–) := by
  -- This is a direct consequence of `UniformFun.comap_eq`
  rw [uniformContinuous_iff]
  exact (UniformOnFun.mono (uniformContinuous_iff.mp hf) subset_rfl).trans_eq UniformOnFun.comap_eq
#align uniform_on_fun.postcomp_uniform_continuous UniformOnFun.postcomp_uniformContinuous

/-- Post-composition by a uniform inducing is a uniform inducing for the
uniform structures of `ğ”–`-convergence.

More precisely, if `f : Î³ â†’ Î²` is a uniform inducing, then
`(fun g â†¦ f âˆ˜ g) : (Î± â†’áµ¤[ğ”–] Î³) â†’ (Î± â†’áµ¤[ğ”–] Î²)` is a uniform inducing. -/
protected theorem postcomp_uniformInducing [UniformSpace Î³] {f : Î³ â†’ Î²} (hf : UniformInducing f) :
    UniformInducing (ofFun ğ”– âˆ˜ (Â· âˆ˜ Â·) f âˆ˜ toFun ğ”–) := by
  -- This is a direct consequence of `UniformFun.comap_eq`
  constructor
  replace hf : (ğ“¤ Î²).comap (Prod.map f f) = _ := hf.comap_uniformity
  change comap (Prod.map (ofFun ğ”– âˆ˜ (Â· âˆ˜ Â·) f âˆ˜ toFun ğ”–) (ofFun ğ”– âˆ˜ (Â· âˆ˜ Â·) f âˆ˜ toFun ğ”–)) _ = _
  rw [â† uniformity_comap] at hf âŠ¢
  congr
  rw [â† uniformSpace_eq hf, UniformOnFun.comap_eq]
  rfl
#align uniform_on_fun.postcomp_uniform_inducing UniformOnFun.postcomp_uniformInducing

/-- Turn a uniform isomorphism `Î³ â‰ƒáµ¤ Î²` into a uniform isomorphism `(Î± â†’áµ¤[ğ”–] Î³) â‰ƒáµ¤ (Î± â†’áµ¤[ğ”–] Î²)`
by post-composing. -/
protected def congrRight [UniformSpace Î³] (e : Î³ â‰ƒáµ¤ Î²) : (Î± â†’áµ¤[ğ”–] Î³) â‰ƒáµ¤ (Î± â†’áµ¤[ğ”–] Î²) :=
  {
    Equiv.piCongrRight fun _a =>
      e.toEquiv with
    uniformContinuous_toFun := UniformOnFun.postcomp_uniformContinuous e.uniformContinuous
    uniformContinuous_invFun := UniformOnFun.postcomp_uniformContinuous e.symm.uniformContinuous }
#align uniform_on_fun.congr_right UniformOnFun.congrRight

/-- Let `f : Î³ â†’ Î±`, `ğ”– : Set (Set Î±)`, `ğ”— : Set (Set Î³)`, and assume that `âˆ€ T âˆˆ ğ”—, f '' T âˆˆ ğ”–`.
Then, the function `(fun g â†¦ g âˆ˜ f) : (Î± â†’áµ¤[ğ”–] Î²) â†’ (Î³ â†’áµ¤[ğ”—] Î²)` is uniformly continuous.

Note that one can easily see that assuming `âˆ€ T âˆˆ ğ”—, âˆƒ S âˆˆ ğ”–, f '' T âŠ† S` would work too, but
we will get this for free when we prove that `ğ’±(Î±, Î², ğ”–, uÎ²) = ğ’±(Î±, Î², ğ”–', uÎ²)` where `ğ”–'` is the
***noncovering*** bornology generated by `ğ”–`. -/
protected theorem precomp_uniformContinuous {ğ”— : Set (Set Î³)} {f : Î³ â†’ Î±}
    (hf : ğ”— âŠ† image f â»Â¹' ğ”–) :
    UniformContinuous fun g : Î± â†’áµ¤[ğ”–] Î² => ofFun ğ”— (g âˆ˜ f) := by
  -- Since `comap` distributes on `iInf`, it suffices to prove that
  -- `â¨… s âˆˆ ğ”–, comap s.restrict ğ’°(â†¥s, Î², uÎ²) â‰¤ â¨… t âˆˆ ğ”—, comap (t.restrict âˆ˜ (â€” âˆ˜ f)) ğ’°(â†¥t, Î², uÎ²)`.
  simp_rw [uniformContinuous_iff, UniformOnFun.uniformSpace, UniformSpace.comap_iInf, â†
    UniformSpace.comap_comap]
  -- For any `t âˆˆ ğ”—`, note `s := f '' t âˆˆ ğ”–`.
  -- We will show that `comap s.restrict ğ’°(â†¥s, Î², uÎ²) â‰¤ comap (t.restrict âˆ˜ (â€” âˆ˜ f)) ğ’°(â†¥t, Î², uÎ²)`.
  refine' le_iInfâ‚‚ fun t ht => iInf_le_of_le (f '' t) <| iInf_le_of_le (hf ht) _
  -- Let `f'` be the map from `t` to `f '' t` induced by `f`.
  let f' : t â†’ f '' t := (mapsTo_image f t).restrict f t (f '' t)
  -- By definition `t.restrict âˆ˜ (â€” âˆ˜ f) = (â€” âˆ˜ f') âˆ˜ (f '' t).restrict`.
  have :
    (t.restrict âˆ˜ fun g : Î± â†’áµ¤[ğ”–] Î² => ofFun ğ”— (g âˆ˜ f)) =
      (fun g : f '' t â†’ Î² => g âˆ˜ f') âˆ˜ (f '' t).restrict :=
    rfl
  -- Thus, we have to show `comap (f '' t).restrict ğ’°(â†¥(f '' t), Î², uÎ²) â‰¤`
  -- `comap (f '' t).restrict (comap (â€” âˆ˜ f') ğ’°(â†¥t, Î², uÎ²))`.
  rw [this, @UniformSpace.comap_comap (Î± â†’áµ¤[ğ”–] Î²) (f '' t â†’áµ¤ Î²)]
  -- But this is exactly monotonicity of `comap` applied to
  -- `UniformFun.precomp_continuous`.
  refine' UniformSpace.comap_mono _
  rw [â† uniformContinuous_iff]
  exact UniformFun.precomp_uniformContinuous
#align uniform_on_fun.precomp_uniform_continuous UniformOnFun.precomp_uniformContinuous

/-- Turn a bijection `e : Î³ â‰ƒ Î±` such that we have both `âˆ€ T âˆˆ ğ”—, e '' T âˆˆ ğ”–` and
`âˆ€ S âˆˆ ğ”–, e â»Â¹' S âˆˆ ğ”—` into a uniform isomorphism `(Î³ â†’áµ¤[ğ”—] Î²) â‰ƒáµ¤ (Î± â†’áµ¤[ğ”–] Î²)` by pre-composing. -/
protected def congrLeft {ğ”— : Set (Set Î³)} (e : Î³ â‰ƒ Î±) (he : ğ”— âŠ† image e â»Â¹' ğ”–)
    (he' : ğ”– âŠ† preimage e â»Â¹' ğ”—) : (Î³ â†’áµ¤[ğ”—] Î²) â‰ƒáµ¤ (Î± â†’áµ¤[ğ”–] Î²) :=
  { Equiv.arrowCongr e
      (Equiv.refl
        _) with
    uniformContinuous_toFun :=
      UniformOnFun.precomp_uniformContinuous
        (by
          intro s hs
          change e.symm '' s âˆˆ ğ”—
          rw [â† preimage_equiv_eq_image_symm]
          exact he' hs)
    uniformContinuous_invFun := UniformOnFun.precomp_uniformContinuous he }
#align uniform_on_fun.congr_left UniformOnFun.congrLeft

/-- If `ğ”–` covers `Î±`, then the topology of `ğ”–`-convergence is Tâ‚‚. -/
theorem t2Space_of_covering [T2Space Î²] (h : â‹ƒâ‚€ ğ”– = univ) : T2Space (Î± â†’áµ¤[ğ”–] Î²) :=
  {
    t2 := by
      intro f g hfg
      obtain âŸ¨x, hxâŸ© := not_forall.mp (mt funext hfg)
      obtain âŸ¨s, hs, hxsâŸ© : âˆƒ s âˆˆ ğ”–, x âˆˆ s := mem_sUnion.mp (h.symm â–¸ True.intro)
      exact separated_by_continuous (uniformContinuous_eval_of_mem Î² ğ”– hxs hs).continuous hx }
#align uniform_on_fun.t2_space_of_covering UniformOnFun.t2Space_of_covering

/-- If `ğ”–` covers `Î±`, the natural map `UniformOnFun.toFun` from `Î± â†’áµ¤[ğ”–] Î²` to `Î± â†’ Î²` is
uniformly continuous.

In other words, if `ğ”–` covers `Î±`, then the uniform structure of `ğ”–`-convergence is finer than
that of pointwise convergence. -/
protected theorem uniformContinuous_toFun (h : â‹ƒâ‚€ ğ”– = univ) :
    UniformContinuous (toFun ğ”– : (Î± â†’áµ¤[ğ”–] Î²) â†’ Î± â†’ Î²) := by
  rw [uniformContinuous_pi]
  intro x
  obtain âŸ¨s : Set Î±, hs : s âˆˆ ğ”–, hxs : x âˆˆ sâŸ© := sUnion_eq_univ_iff.mp h x
  exact uniformContinuous_eval_of_mem Î² ğ”– hxs hs
#align uniform_on_fun.uniform_continuous_to_fun UniformOnFun.uniformContinuous_toFun

/-- Convergence in the topology of `ğ”–`-convergence means uniform convergence on `S` (in the sense
of `TendstoUniformlyOn`) for all `S âˆˆ ğ”–`. -/
protected theorem tendsto_iff_tendstoUniformlyOn {F : Î¹ â†’ Î± â†’áµ¤[ğ”–] Î²} {f : Î± â†’áµ¤[ğ”–] Î²} :
    Tendsto F p (ğ“ f) â†” âˆ€ s âˆˆ ğ”–, TendstoUniformlyOn F f p s := by
  rw [UniformOnFun.topologicalSpace_eq, nhds_iInf, tendsto_iInf]
  refine' forall_congr' fun s => _
  rw [nhds_iInf, tendsto_iInf]
  refine' forall_congr' fun hs => _
  rw [nhds_induced (T := _), tendsto_comap_iff, tendstoUniformlyOn_iff_tendstoUniformly_comp_coe,
    UniformFun.tendsto_iff_tendstoUniformly]
  rfl
#align uniform_on_fun.tendsto_iff_tendsto_uniformly_on UniformOnFun.tendsto_iff_tendstoUniformlyOn

/-- The natural bijection between `Î± â†’ Î² Ã— Î³` and `(Î± â†’ Î²) Ã— (Î± â†’ Î³)`, upgraded to a uniform
isomorphism between `Î± â†’áµ¤[ğ”–] Î² Ã— Î³` and `(Î± â†’áµ¤[ğ”–] Î²) Ã— (Î± â†’áµ¤[ğ”–] Î³)`. -/
protected def uniformEquivProdArrow [UniformSpace Î³] :
    (Î± â†’áµ¤[ğ”–] Î² Ã— Î³) â‰ƒáµ¤ (Î± â†’áµ¤[ğ”–] Î²) Ã— (Î± â†’áµ¤[ğ”–] Î³) :=
  -- Denote `Ï†` this bijection. We want to show that
  -- `comap Ï† (ğ’±(Î±, Î², ğ”–, uÎ²) Ã— ğ’±(Î±, Î³, ğ”–, uÎ³)) = ğ’±(Î±, Î² Ã— Î³, ğ”–, uÎ² Ã— uÎ³)`.
  -- But `uÎ² Ã— uÎ³` is defined as `comap fst uÎ² âŠ“ comap snd uÎ³`, so we just have to apply
  -- `UniformOnFun.inf_eq` and `UniformOnFun.comap_eq`,
  -- which leaves us to check that some square commutes.
  -- We could also deduce this from `UniformFun.uniformEquivProdArrow`,
  -- but it turns out to be more annoying.
  ((UniformOnFun.ofFun ğ”–).symm.trans <|
    (Equiv.arrowProdEquivProdArrow _ _ _).trans <|
      (UniformOnFun.ofFun ğ”–).prodCongr (UniformOnFun.ofFun ğ”–)).toUniformEquivOfUniformInducing $ by
      constructor
      rw [uniformity_prod, comap_inf, comap_comap, comap_comap]
      have H := @UniformOnFun.inf_eq Î± (Î² Ã— Î³) ğ”–
        (UniformSpace.comap Prod.fst â€¹_â€º) (UniformSpace.comap Prod.snd â€¹_â€º)
      apply_fun (fun u â†¦ @uniformity (Î± â†’áµ¤[ğ”–] Î² Ã— Î³) u) at H
      convert H.symm using 1
      rw [UniformOnFun.comap_eq, UniformOnFun.comap_eq]
      erw [inf_uniformity]
      rw [uniformity_comap, uniformity_comap]
      rfl
#align uniform_on_fun.uniform_equiv_prod_arrow UniformOnFun.uniformEquivProdArrow

-- the relevant diagram commutes by definition
variable (ğ”–) (Î´ : Î¹ â†’ Type*) [âˆ€ i, UniformSpace (Î´ i)]

/-- The natural bijection between `Î± â†’ Î  i, Î´ i` and `Î  i, Î± â†’ Î´ i`, upgraded to a uniform
isomorphism between `Î± â†’áµ¤[ğ”–] (Î  i, Î´ i)` and `Î  i, Î± â†’áµ¤[ğ”–] Î´ i`. -/
protected def uniformEquivPiComm : (Î± â†’áµ¤[ğ”–] ((i:Î¹) â†’ Î´ i)) â‰ƒáµ¤ ((i:Î¹) â†’ Î± â†’áµ¤[ğ”–] Î´ i) :=
  -- Denote `Ï†` this bijection. We want to show that
  -- `comap Ï† (Î  i, ğ’±(Î±, Î´ i, ğ”–, uÎ´ i)) = ğ’±(Î±, (Î  i, Î´ i), ğ”–, (Î  i, uÎ´ i))`.
  -- But `Î  i, uÎ´ i` is defined as `â¨… i, comap (eval i) (uÎ´ i)`, so we just have to apply
  -- `UniformOnFun.iInf_eq` and `UniformOnFun.comap_eq`,
  -- which leaves us to check that some square commutes.
  -- We could also deduce this from `UniformFun.uniformEquivPiComm`, but it turns out
  -- to be more annoying.
  @Equiv.toUniformEquivOfUniformInducing (Î± â†’áµ¤[ğ”–] ((i:Î¹) â†’ Î´ i)) ((i:Î¹) â†’ Î± â†’áµ¤[ğ”–] Î´ i)
      _ _ (Equiv.piComm _) $ by
    constructor
    change comap (Prod.map Function.swap Function.swap) _ = _
    erw [â† uniformity_comap]
    congr
    rw [Pi.uniformSpace, UniformSpace.ofCoreEq_toCore, Pi.uniformSpace,
      UniformSpace.ofCoreEq_toCore, UniformSpace.comap_iInf, UniformOnFun.iInf_eq]
    refine' iInf_congr fun i => _
    rw [â† UniformSpace.comap_comap, UniformOnFun.comap_eq]
    rfl
#align uniform_on_fun.uniform_equiv_Pi_comm UniformOnFun.uniformEquivPiComm

-- Like in the previous lemma, the diagram actually commutes by definition
end UniformOnFun
