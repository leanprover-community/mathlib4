/-
Copyright (c) 2025 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.Topology.Algebra.Indicator
import Mathlib.Topology.Separation.DisjointCover
import Mathlib.Order.Disjointed
import Mathlib.Topology.ContinuousMap.Algebra
import Mathlib.Topology.UniformSpace.OfCompactT2

/-!
# Approximation by locally constant functions

We show that if `X` is a profinite space, then any continuous function from `X` to a T2 space
can be uniformly approximated by locally constant functions.
-/

open Set

open TopologicalSpace (Opens Clopens IsOpenCover)

open scoped Function Uniformity

namespace ContinuousMap

variable {X V : Type*}
  [TopologicalSpace X] [TotallyDisconnectedSpace X] [T2Space X] [CompactSpace X]
  [TopologicalSpace V] [T2Space V] (f : C(X, V)) {S : Set (V √ó V)}

-- this lemma is private since it is immediately superseded by the next lemma
private lemma exists_disjoint_nonempty_clopen_cover_of_mem_nhds_diagonal_of_compact [CompactSpace V]
    (hS : S ‚àà nhdsSet (.diagonal V)) :
    ‚àÉ (n : ‚Ñï) (D : Fin n ‚Üí Clopens X), (‚àÄ i, D i ‚â† ‚ä•) ‚àß
    (‚àÄ i, ‚àÄ y ‚àà D i, ‚àÄ z ‚àà D i, (f y, f z) ‚àà S) ‚àß (univ : Set X) ‚äÜ ‚ãÉ i, ‚Üë(D i) ‚àß
    Pairwise (Disjoint on D) := by
  letI : UniformSpace V := uniformSpaceOfCompactT2
  -- Find a neighbourhood of each point on which `f` varies within `S`
  have step1 (x) : ‚àÉ U : Opens X, x ‚àà U ‚àß ‚àÄ y ‚àà U, ‚àÄ z ‚àà U, (f y, f z) ‚àà S := by
    obtain ‚ü®R, hR, hR', hRS‚ü© := comp_symm_of_uniformity hS
    obtain ‚ü®U, hUB, hUo, hUx‚ü© := mem_nhds_iff.mp <|  (f.continuousAt x).preimage_mem_nhds
      <| UniformSpace.ball_mem_nhds _ hR
    exact ‚ü®‚ü®U, hUo‚ü©, hUx, fun y hy z hz ‚Ü¶ hRS <| prodMk_mem_compRel (hR' <| hUB hy) (hUB hz)‚ü©
  choose U hUx hUS using step1
  have hUc : IsOpenCover U := by ext x; simpa using ‚ü®x, hUx x‚ü©
  -- Now refine it to a disjoint covering.
  obtain ‚ü®n, W, hW‚ÇÅ, hW‚ÇÇ, hW‚ÇÉ‚ü© := hUc.exists_finite_nonempty_disjoint_clopen_cover
  refine ‚ü®n, W, fun j ‚Ü¶ (hW‚ÇÅ j).1, fun j y hy z hz ‚Ü¶ ?_, hW‚ÇÇ, hW‚ÇÉ‚ü©
  exact match (hW‚ÇÅ j).2 with | ‚ü®i, hi‚ü© => hUS i y (hi hy) z (hi hz)

/--
For any continuous function `X ‚Üí V`, with `X` profinite and `V` Hausdorff, and `S` a
neighbourhood of the diagonal in `V √ó V`, there exists a finite cover of `X` by pairwise-disjoint
nonempty clopens, on each of which `f` varies within `S`.
-/
lemma exists_disjoint_nonempty_clopen_cover_of_mem_nhds_diagonal (hS : S ‚àà nhdsSet (.diagonal V)) :
    ‚àÉ (n : ‚Ñï) (D : Fin n ‚Üí Clopens X), (‚àÄ i, D i ‚â† ‚ä•) ‚àß
    (‚àÄ i, ‚àÄ y ‚àà D i, ‚àÄ z ‚àà D i, (f y, f z) ‚àà S) ‚àß (univ : Set X) ‚äÜ ‚ãÉ i, ‚Üë(D i) ‚àß
    Pairwise (Disjoint on D) := by
  -- reduce to compact case by considering range of `f`
  let V' := Set.range f
  haveI : CompactSpace V' := isCompact_iff_compactSpace.mp <| isCompact_range (map_continuous f)
  let g : C(X, V') :=
  { toFun := V'.codRestrict f Set.mem_range_self, continuous_toFun := by fun_prop }
  suffices S.preimage (Prod.map (‚Üë) (‚Üë)) ‚àà nhdsSet (.diagonal V') from
    exists_disjoint_nonempty_clopen_cover_of_mem_nhds_diagonal_of_compact g this
  rw [mem_nhdsSet_iff_forall] at hS ‚ä¢
  refine fun ‚ü®x, y‚ü© hxy ‚Ü¶ ContinuousAt.preimage_mem_nhds ?_ (hS ‚ü®x, y‚ü© (Subtype.val_inj.mpr hxy))
  exact continuousAt_subtype_val.prodMap continuousAt_subtype_val

/--
Any continuous function from a profinite space to T2 space can be approximated by a function
factoring through `Fin n`, for some `n`.
-/
lemma exists_finite_approximation_of_mem_nhds_diagonal (hS : S ‚àà nhdsSet (.diagonal V)) :
    ‚àÉ (n : ‚Ñï) (g : X ‚Üí Fin n) (h : Fin n ‚Üí V), Continuous g ‚àß ‚àÄ x, (f x, h (g x)) ‚àà S := by
  obtain ‚ü®n, E, hEne, hES, hEuniv, hEdis‚ü© :=
    exists_disjoint_nonempty_clopen_cover_of_mem_nhds_diagonal f hS
  have h_uniq (x) : ‚àÉ! i, x ‚àà E i := by
    refine match Set.mem_iUnion.mp (hEuniv <| mem_univ x) with
      | ‚ü®i, hi‚ü© => ‚ü®i, hi, fun j hj ‚Ü¶ hEdis.eq ?_‚ü©
    simpa [‚Üê Clopens.coe_disjoint, not_disjoint_iff] using ‚ü®x, hj, hi‚ü©
  choose g hg hg' using h_uniq -- for each `x`, `g x` is the unique `i` such that `x ‚àà E i`
  have h_ex (i) : ‚àÉ x, x ‚àà E i := by
    simpa [‚Üê SetLike.coe_set_eq, ‚Üê nonempty_iff_ne_empty] using hEne i
  choose r hr using h_ex -- for each `i`, choose an `r i ‚àà E i`
  refine ‚ü®n, g, f ‚àò r, continuous_discrete_rng.mpr fun j ‚Ü¶ ?_, fun x ‚Ü¶ (hES _) _ (hg _) _ (hr _)‚ü©
  convert (E j).isOpen
  exact Set.ext fun x ‚Ü¶ ‚ü®fun hj ‚Ü¶ hj ‚ñ∏ hg x, fun hx ‚Ü¶ (hg' _ _ hx).symm‚ü©

/--
If `f` is a continuous map from a profinite space to a uniform space with a group structure, then
we can uniformly approximate `f` by finite products of indicator functions of clopen sets.

(Note no compatibility is assumed between the group structure on `V` and the uniform structure.)
-/
@[to_additive "If `f` is a continuous map from a profinite space to a uniform space with an
additive group structure, then we can uniformly approximate `f` by finite sums of indicator
functions of clopen sets.

(Note no compatibility is assumed between the group structure on `V` and the uniform structure.)"]
lemma exists_finite_sum_const_mulIndicator_approximation_of_mem_nhds_diagonal [CommMonoid V]
    (hS : S ‚àà nhdsSet (.diagonal V)) :
    ‚àÉ (n : ‚Ñï) (U : Fin n ‚Üí Clopens X) (v : Fin n ‚Üí V),
    ‚àÄ x, (f x, ‚àè n, mulIndicator (U n) (fun _ ‚Ü¶ v n) x) ‚àà S := by
  obtain ‚ü®n, g, h, hg, hgh‚ü© := exists_finite_approximation_of_mem_nhds_diagonal f hS
  refine ‚ü®n, fun i ‚Ü¶ ‚ü®_, (isClopen_discrete {i}).preimage hg‚ü©, h, fun x ‚Ü¶ ?_‚ü©
  convert hgh x
  exact (Fintype.prod_eq_single _ fun i hi ‚Ü¶ mulIndicator_of_not_mem hi.symm _).trans
    (mulIndicator_of_mem rfl _)

/-!
## Functions on product spaces
-/
section product
variable {R Y V : Type*} [TopologicalSpace Y] [CompactSpace Y]
  [AddCommGroup V] [UniformSpace V] [IsUniformAddGroup V] [T2Space V] {S : Set (V √ó V)}

/-- A continuous function on `X √ó Y` can be uniformly approximated by sums of functions of the
form `f x ‚Ä¢ g y`. -/
lemma exists_finite_sum_smul_approximation_of_mem_uniformity [TopologicalSpace R]
    [MonoidWithZero R] [MulActionWithZero R V] (f : C(X √ó Y, V)) (hS : S ‚àà ùì§ V) :
    ‚àÉ (n : ‚Ñï) (g : Fin n ‚Üí C(X, R)) (h : Fin n ‚Üí C(Y, V)),
    ‚àÄ x y, (f (x, y), ‚àë i, g i x ‚Ä¢ h i y) ‚àà S := by
  have hS' : {(f, g) | ‚àÄ y, (f y, g y) ‚àà S} ‚àà ùì§ C(Y, V) :=
    (mem_compactConvergence_entourage_iff _).mpr
      ‚ü®_, _, isCompact_univ, hS, by simp only [Set.mem_univ, true_implies, subset_refl]‚ü©
  obtain ‚ü®n, U, v, hv‚ü© := exists_finite_sum_const_indicator_approximation_of_mem_nhds_diagonal
    f.curry (nhdsSet_diagonal_le_uniformity hS')
  refine ‚ü®n, fun i ‚Ü¶ ‚ü®_, (U i).isClopen.continuous_indicator <| continuous_const (y := 1)‚ü©,
    v, fun x y ‚Ü¶ ?_‚ü©
  convert hv x y using 2
  simp only [sum_apply]
  congr 1 with i
  by_cases hi : x ‚àà U i <;> simp [hi]

/-- A continuous function on `X √ó Y` can be uniformly approximated by sums of functions of the form
`f x * g y`. -/
lemma exists_finite_sum_mul_approximation_of_mem_uniformity [Ring R] [UniformSpace R]
    [IsUniformAddGroup R] [T2Space R] (f : C(X √ó Y, R)) {S : Set (R √ó R)} (hS : S ‚àà ùì§ R) :
    ‚àÉ (n : ‚Ñï) (g : Fin n ‚Üí C(X, R)) (h : Fin n ‚Üí C(Y, R)),
    ‚àÄ x y, (f (x, y), ‚àë i, g i x * h i y) ‚àà S :=
  exists_finite_sum_smul_approximation_of_mem_uniformity f hS

end product

end ContinuousMap
