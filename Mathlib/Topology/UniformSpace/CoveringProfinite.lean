/-
Copyright (c) 2025 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.Topology.Algebra.Indicator
import Mathlib.Topology.Separation.DisjointCover
import Mathlib.Order.Disjointed
import Mathlib.Topology.ContinuousMap.Algebra

/-!
# Uniform approximation by locally constant functions

We show that if `X` is a profinite space, and `V` is a uniform space, then any continuous function
`X ‚Üí V` can be uniformly approximated by locally constant functions.
-/

open UniformSpace Set

open TopologicalSpace (Opens Clopens IsOpenCover)

open scoped Uniformity Function

namespace ContinuousMap

variable {X Y V : Type*}
  [TopologicalSpace X] [TotallyDisconnectedSpace X] [T2Space X] [CompactSpace X]
  [UniformSpace V] (f : C(X, V)) {S : Set (V √ó V)}

/--
For any continuous function on `X` and `S ‚àà ùì§ V`, there exists a finite cover of `X` by
pairwise-disjoint nonempty clopens, on each of which `f` varies within `S`.
-/
lemma exists_disjoint_nonempty_clopen_cover_of_mem_uniformity (hS : S ‚àà ùì§ V) :
    ‚àÉ (n : ‚Ñï) (D : Fin n ‚Üí Clopens X), (‚àÄ i, D i ‚â† ‚ä•) ‚àß
    (‚àÄ i, ‚àÄ y ‚àà D i, ‚àÄ z ‚àà D i, (f y, f z) ‚àà S) ‚àß (univ : Set X) ‚äÜ ‚ãÉ i, ‚Üë(D i) ‚àß
    Pairwise (Disjoint on D) := by
  -- Find a neighbourhood of each point on which `f` varies within `S`
  have step1 (x) : ‚àÉ U : Opens X, x ‚àà U ‚àß ‚àÄ y ‚àà U, ‚àÄ z ‚àà U, (f y, f z) ‚àà S := by
    obtain ‚ü®R, hR, hR', hRS‚ü© := comp_symm_of_uniformity hS
    obtain ‚ü®U, hUB, hUo, hUx‚ü© := mem_nhds_iff.mp <|  (f.continuousAt x).preimage_mem_nhds
      <| UniformSpace.ball_mem_nhds _ hR
    exact ‚ü®‚ü®U, hUo‚ü©, hUx, fun y hy z hz ‚Ü¶ hRS <| prodMk_mem_compRel (hR' <| hUB hy) (hUB hz)‚ü©
  choose U hUx hUS using step1
  have hUc : IsOpenCover U := by ext x; simpa using ‚ü®x, hUx x‚ü©
  -- Now refine it to a disjoint covering.
  obtain ‚ü®n, W, hW‚ÇÅ, hW‚ÇÇ, hW‚ÇÉ‚ü© := hUc.exists_finite_nonempty_disjoint_clopen_cover
  refine ‚ü®n, W, fun j ‚Ü¶ (hW‚ÇÅ j).1, fun j y hy z hz ‚Ü¶ ?_, hW‚ÇÇ, hW‚ÇÉ‚ü©
  exact match (hW‚ÇÅ j).2 with | ‚ü®i, hi‚ü© => hUS i y (hi hy) z (hi hz)

/--
Any continuous function from a profinite space to a uniform space can be uniformly approximated
by a function factoring through `Fin n`, for some `n`.
-/
lemma exists_fin_comp_of_mem_uniformity (hS : S ‚àà ùì§ V) :
    ‚àÉ (n : ‚Ñï) (g : X ‚Üí Fin n) (h : Fin n ‚Üí V), Continuous g ‚àß ‚àÄ x, (f x, h (g x)) ‚àà S := by
  classical
  obtain ‚ü®n, E, hEne, hES, hEuniv, hEdis‚ü© :=
    exists_disjoint_nonempty_clopen_cover_of_mem_uniformity f hS
  have h_uniq (x) : ‚àÉ! i, x ‚àà E i := by
    refine match Set.mem_iUnion.mp (hEuniv <| mem_univ x) with
      | ‚ü®i, hi‚ü© => ‚ü®i, hi, fun j hj ‚Ü¶ hEdis.eq ?_‚ü©
    simpa [‚Üê Clopens.coe_disjoint, not_disjoint_iff] using ‚ü®x, hj, hi‚ü©
  choose g hg hg' using h_uniq -- for each `x`, `g x` is the unique `i` such that `x ‚àà E i`
  have h_ex (i) : ‚àÉ x, x ‚àà E i := by
    simpa [‚Üê SetLike.coe_set_eq, ‚Üê nonempty_iff_ne_empty] using hEne i
  choose r hr using h_ex -- for each `i`, choose an `r i ‚àà E i`
  refine ‚ü®n, g, f ‚àò r, continuous_discrete_rng.mpr fun j ‚Ü¶ ?_, fun x ‚Ü¶ (hES _) _ (hg _) _ (hr _)‚ü©
  convert (E j).isOpen
  exact Set.ext fun x ‚Ü¶ ‚ü®fun hj ‚Ü¶ hj ‚ñ∏ hg x, fun hx ‚Ü¶ (hg' _ _ hx).symm‚ü©

/--
If `f` is a continuous map from a profinite space to a uniform space with a group structure, then
we can uniformly approximate `f` by finite products of indicator functions of clopen sets.
-/
@[to_additive "If `f` is a continuous map from a profinite space to a uniform space with an
additive group structure, then we can uniformly approximate `f` by finite sums of indicator
functions of clopen sets."]
lemma exists_sum_const_mulIndicator_approx [CommMonoid V] (hS : S ‚àà ùì§ V) :
    ‚àÉ (n : ‚Ñï) (U : Fin n ‚Üí Clopens X) (v : Fin n ‚Üí V),
    ‚àÄ x, (f x, ‚àè n, mulIndicator (U n) (fun _ ‚Ü¶ v n) x) ‚àà S := by
  obtain ‚ü®n, g, h, hg, hgh‚ü© := exists_fin_comp_of_mem_uniformity f hS
  refine ‚ü®n, fun i ‚Ü¶ ‚ü®_, (isClopen_discrete {i}).preimage hg‚ü©, h, fun x ‚Ü¶ ?_‚ü©
  convert hgh x
  exact (Fintype.prod_eq_single _ fun i hi ‚Ü¶ mulIndicator_of_not_mem hi.symm _).trans
    (mulIndicator_of_mem rfl _)

variable {R Y : Type*} [TopologicalSpace Y] [CompactSpace Y]
  [TopologicalSpace R] [MonoidWithZero R]

/-- A continuous function on `X √ó Y` can be uniformly approximated by sums of functions of the
form `f x ‚Ä¢ g y`. -/
lemma exists_sum_smul_approx [AddCommGroup V] [IsUniformAddGroup V] [MulActionWithZero R V]
    (f : C(X √ó Y, V)) (hS : S ‚àà ùì§ V) :
    ‚àÉ (n : ‚Ñï) (g : Fin n ‚Üí C(X, R)) (h : Fin n ‚Üí C(Y, V)),
    ‚àÄ x y, (f (x, y), ‚àë i, g i x ‚Ä¢ h i y) ‚àà S := by
  have hS' : {(f, g) | ‚àÄ y, (f y, g y) ‚àà S} ‚àà ùì§ C(Y, V) :=
    (mem_compactConvergence_entourage_iff _).mpr
      ‚ü®_, _, isCompact_univ, hS, by simp only [Set.mem_univ, true_implies, subset_refl]‚ü©
  obtain ‚ü®n, U, v, hv‚ü© := exists_sum_const_indicator_approx f.curry hS'
  refine ‚ü®n, fun i ‚Ü¶ ‚ü®_, (U i).isClopen.continuous_indicator <| continuous_const (y := 1)‚ü©,
    v, fun x y ‚Ü¶ ?_‚ü©
  convert hv x y using 2
  simp only [sum_apply]
  congr 1 with i
  by_cases hi : x ‚àà U i <;> simp [hi]

/-- A continuous function on `X √ó Y` can be uniformly approximated by sums of functions of the form
`f x * g y`. -/
lemma exists_sum_mul_approx (f : C(X √ó Y, V)) (hS : S ‚àà ùì§ V) [Ring V] [IsUniformAddGroup V]:
    ‚àÉ (n : ‚Ñï) (g : Fin n ‚Üí C(X, V)) (h : Fin n ‚Üí C(Y, V)),
    ‚àÄ x y, (f (x, y), ‚àë i, g i x * h i y) ‚àà S :=
  exists_sum_smul_approx f hS

end ContinuousMap
