/-
Copyright (c) 2025 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.Topology.Algebra.Indicator
import Mathlib.Topology.ContinuousMap.Algebra
import Mathlib.Topology.Separation.DisjointCover

/-!
# Uniform approximation by products

We show that if `X, Y` are compact Hausdorff spaces with `X` profinite, then any continuous function
on `X √ó Y` valued in a ring (with a uniform structure) can be uniformly approximated by finite
sums of functions of the form `f x * g y`.
-/

open UniformSpace

open scoped Uniformity

namespace ContinuousMap

variable {X Y R V : Type*}
  [TopologicalSpace X] [TotallyDisconnectedSpace X] [T2Space X] [CompactSpace X]
  [TopologicalSpace Y] [CompactSpace Y]
  [AddCommGroup V] [UniformSpace V] [IsUniformAddGroup V] {S : Set (V √ó V)}

/-- A continuous function on `X √ó Y`, taking values in an `R`-module with a uniform structure,
can be uniformly approximated by sums of functions of the form `(x, y) ‚Ü¶ f x ‚Ä¢ g y`.

Note that no continuity properties are assumed either for multiplication on `R`, or for the scalar
multiplication of `R` on `V`. -/
lemma exists_finite_sum_smul_approximation_of_mem_uniformity [TopologicalSpace R]
    [MonoidWithZero R] [MulActionWithZero R V] (f : C(X √ó Y, V)) (hS : S ‚àà ùì§ V) :
    ‚àÉ (n : ‚Ñï) (g : Fin n ‚Üí C(X, R)) (h : Fin n ‚Üí C(Y, V)),
    ‚àÄ x y, (f (x, y), ‚àë i, g i x ‚Ä¢ h i y) ‚àà S := by
  have hS' : {(f, g) | ‚àÄ y, (f y, g y) ‚àà S} ‚àà ùì§ C(Y, V) :=
    (mem_compactConvergence_entourage_iff _).mpr
      ‚ü®_, _, isCompact_univ, hS, by simp only [Set.mem_univ, true_implies, subset_refl]‚ü©
  obtain ‚ü®n, U, v, hv‚ü© := exists_finite_sum_const_indicator_approximation_of_mem_nhds_diagonal
    f.curry (nhdsSet_diagonal_le_uniformity hS')
  refine ‚ü®n, fun i ‚Ü¶ ‚ü®_, (U i).isClopen.continuous_indicator <| continuous_const (y := 1)‚ü©,
    v, fun x y ‚Ü¶ ?_‚ü©
  convert hv x y using 2
  simp only [sum_apply]
  congr 1 with i
  by_cases hi : x ‚àà U i <;> simp [hi]

/-- A continuous function on `X √ó Y`, taking values in a ring `R` equipped with a uniformity
compatible with addition, can be uniformly approximated by sums of functions of the form
`(x, y) ‚Ü¶ f x * g y`.

Note that no assumption is needed relating the multiplication on `R` to the uniformity. -/
lemma exists_finite_sum_mul_approximation_of_mem_uniformity [Ring R] [UniformSpace R]
    [IsUniformAddGroup R] (f : C(X √ó Y, R)) {S : Set (R √ó R)} (hS : S ‚àà ùì§ R) :
    ‚àÉ (n : ‚Ñï) (g : Fin n ‚Üí C(X, R)) (h : Fin n ‚Üí C(Y, R)),
    ‚àÄ x y, (f (x, y), ‚àë i, g i x * h i y) ‚àà S :=
  exists_finite_sum_smul_approximation_of_mem_uniformity f hS

end ContinuousMap
