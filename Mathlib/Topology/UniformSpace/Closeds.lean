/-
Copyright (c) 2025 Attila G√°sp√°r. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Attila G√°sp√°r
-/
module

public import Mathlib.Topology.Order.Lattice
public import Mathlib.Topology.Sets.VietorisTopology
public import Mathlib.Topology.UniformSpace.UniformEmbedding

import Mathlib.Topology.UniformSpace.Compact

/-!
# Hausdorff uniformity

This file defines the Hausdorff uniformity on the types of closed subsets, compact subsets and
and nonempty compact subsets of a uniform space. This is the generalization of the uniformity
induced by the Hausdorff metric to hyperspaces of uniform spaces.
-/

@[expose] public section

open Topology
open scoped Uniformity

variable {Œ± Œ≤ : Type*}

section hausdorffEntourage

open SetRel

/-- The set of pairs of sets contained in each other's thickening with respect to an entourage. -/
def hausdorffEntourage (U : SetRel Œ± Œ±) : SetRel (Set Œ±) (Set Œ±) :=
  {x | x.1 ‚äÜ U.preimage x.2 ‚àß x.2 ‚äÜ U.image x.1}

theorem mem_hausdorffEntourage (U : SetRel Œ± Œ±) (s t : Set Œ±) :
    (s, t) ‚àà hausdorffEntourage U ‚Üî s ‚äÜ U.preimage t ‚àß t ‚äÜ U.image s :=
  Iff.rfl

@[gcongr]
theorem hausdorffEntourage_mono {U V : SetRel Œ± Œ±} (h : U ‚äÜ V) :
    hausdorffEntourage U ‚äÜ hausdorffEntourage V := by
  unfold hausdorffEntourage
  gcongr

theorem monotone_hausdorffEntourage : Monotone (hausdorffEntourage (Œ± := Œ±)) :=
  fun _ _ => hausdorffEntourage_mono

instance isRefl_hausdorffEntourage (U : SetRel Œ± Œ±) [U.IsRefl] :
    (hausdorffEntourage U).IsRefl :=
  ‚ü®fun _ => ‚ü®U.self_subset_preimage _, U.self_subset_image _‚ü©‚ü©

@[simp]
theorem inv_hausdorffEntourage (U : SetRel Œ± Œ±) :
    (hausdorffEntourage U).inv = hausdorffEntourage U.inv :=
  Set.ext fun _ => And.comm

instance isSymm_hausdorffEntourage (U : SetRel Œ± Œ±) [U.IsSymm] :
    (hausdorffEntourage U).IsSymm := by
  rw [‚Üê inv_eq_self_iff, inv_hausdorffEntourage, inv_eq_self]

theorem hausdorffEntourage_comp (U V : SetRel Œ± Œ±) :
    hausdorffEntourage (U ‚óã V) = hausdorffEntourage U ‚óã hausdorffEntourage V := by
  apply subset_antisymm
  ¬∑ intro ‚ü®s, t‚ü© ‚ü®hst, hts‚ü©
    simp only [mem_comp, mem_hausdorffEntourage] at *
    refine ‚ü®U.image s ‚à© V.preimage t, ‚ü®?_, Set.inter_subset_left‚ü©, ‚ü®Set.inter_subset_right, ?_‚ü©‚ü©
    ¬∑ intro x hx
      obtain ‚ü®z, hz, y, hxy, hyz‚ü© := hst hx
      exact ‚ü®y, ‚ü®‚ü®x, hx, hxy‚ü©, ‚ü®z, hz, hyz‚ü©‚ü©, hxy‚ü©
    ¬∑ intro z hz
      obtain ‚ü®x, hx, y, hxy, hyz‚ü© := hts hz
      exact ‚ü®y, ‚ü®‚ü®x, hx, hxy‚ü©, ‚ü®z, hz, hyz‚ü©‚ü©, hyz‚ü©
  ¬∑ intro ‚ü®s‚ÇÅ, s‚ÇÉ‚ü© ‚ü®s‚ÇÇ, ‚ü®h‚ÇÅ‚ÇÇ, h‚ÇÇ‚ÇÅ‚ü©, ‚ü®h‚ÇÇ‚ÇÉ, h‚ÇÉ‚ÇÇ‚ü©‚ü©
    simp only at *
    grw [mem_hausdorffEntourage, preimage_comp, ‚Üê h‚ÇÇ‚ÇÉ, ‚Üê h‚ÇÅ‚ÇÇ, image_comp, ‚Üê h‚ÇÇ‚ÇÅ, ‚Üê h‚ÇÉ‚ÇÇ]
    exact ‚ü®subset_rfl, subset_rfl‚ü©

instance isTrans_hausdorffEntourage (U : SetRel Œ± Œ±) [U.IsTrans] :
    (hausdorffEntourage U).IsTrans := by
  grw [isTrans_iff_comp_subset_self, ‚Üê hausdorffEntourage_comp, comp_subset_self]

@[simp]
theorem singleton_mem_hausdorffEntourage (U : SetRel Œ± Œ±) (x y : Œ±) :
    ({x}, {y}) ‚àà hausdorffEntourage U ‚Üî (x, y) ‚àà U := by
  simp [hausdorffEntourage]

theorem union_mem_hausdorffEntourage (U : SetRel Œ± Œ±) {s‚ÇÅ s‚ÇÇ t‚ÇÅ t‚ÇÇ : Set Œ±}
    (h‚ÇÅ : (s‚ÇÅ, t‚ÇÅ) ‚àà hausdorffEntourage U) (h‚ÇÇ : (s‚ÇÇ, t‚ÇÇ) ‚àà hausdorffEntourage U) :
    (s‚ÇÅ ‚à™ s‚ÇÇ, t‚ÇÅ ‚à™ t‚ÇÇ) ‚àà hausdorffEntourage U := by
  grind [mem_hausdorffEntourage, preimage_union, image_union]

theorem TotallyBounded.exists_prodMk_finset_mem_hausdorffEntourage [UniformSpace Œ±]
    {s : Set Œ±} (hs : TotallyBounded s) {U : SetRel Œ± Œ±} (hU : U ‚àà ùì§ Œ±) :
    ‚àÉ t : Finset Œ±, (‚Üët, s) ‚àà hausdorffEntourage U := by
  obtain ‚ü®t, ht‚ÇÅ, ht‚ÇÇ‚ü© := hs _ (symm_le_uniformity hU)
  lift t to Finset Œ± using ht‚ÇÅ
  classical
  refine ‚ü®{x ‚àà t | ‚àÉ y ‚àà s, (x, y) ‚àà U}, ?_‚ü©
  rw [Finset.coe_filter]
  refine ‚ü®fun _ h => h.2, fun x hx => ?_‚ü©
  obtain ‚ü®y, hy, hxy‚ü© := Set.mem_iUnion‚ÇÇ.mp (ht‚ÇÇ hx)
  exact ‚ü®y, ‚ü®hy, x, hx, hxy‚ü©, hxy‚ü©

end hausdorffEntourage

variable [UniformSpace Œ±] [UniformSpace Œ≤]

variable (Œ±) in
/-- The Hausdorff uniformity on the powerset of a uniform space. Used for defining the uniformities
on `Closeds`, `Compacts` and `NonemptyCompacts`.
See note [reducible non-instances]. -/
protected abbrev UniformSpace.hausdorff : UniformSpace (Set Œ±) := .ofCore
  { uniformity := (ùì§ Œ±).lift' hausdorffEntourage
    refl := by
      simp_rw [Filter.principal_le_lift', SetRel.id_subset_iff]
      intro (U : SetRel Œ± Œ±) hU
      have := isRefl_of_mem_uniformity hU
      exact isRefl_hausdorffEntourage U
    symm :=
      Filter.tendsto_lift'.mpr fun U hU => Filter.mem_of_superset
        (Filter.mem_lift' (symm_le_uniformity hU)) (inv_hausdorffEntourage U).symm.subset
    comp := by
      rw [Filter.le_lift']
      intro U hU
      obtain ‚ü®V, hV, hVU‚ü© := comp_mem_uniformity_sets hU
      refine Filter.mem_of_superset (Filter.mem_lift' (Filter.mem_lift' hV)) ?_
      grw [‚Üê hausdorffEntourage_comp, hVU] }

attribute [local instance] UniformSpace.hausdorff

theorem Filter.HasBasis.uniformity_hausdorff
    {Œπ : Sort*} {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí Set (Œ± √ó Œ±)} (h : (ùì§ Œ±).HasBasis p s) :
    (ùì§ (Set Œ±)).HasBasis p (hausdorffEntourage ‚àò s) :=
  h.lift' monotone_hausdorffEntourage

namespace UniformSpace.hausdorff

theorem isOpen_inter_nonempty_of_isOpen {U : Set Œ±} (hU : IsOpen U) :
    IsOpen {s | (s ‚à© U).Nonempty} := by
  rw [isOpen_iff_mem_nhds]
  intro s ‚ü®x, hx‚ÇÅ, hx‚ÇÇ‚ü©
  rw [‚Üê hU.mem_nhds_iff, mem_nhds_iff] at hx‚ÇÇ
  obtain ‚ü®V, hV, hVU‚ü© := hx‚ÇÇ
  rw [mem_nhds_iff]
  refine ‚ü®_, Filter.mem_lift' hV, ?_‚ü©
  rintro s' ‚ü®hs', -‚ü©
  obtain ‚ü®y, hy, hxy‚ü© := hs' hx‚ÇÅ
  exact ‚ü®y, hy, hVU hxy‚ü©

/-- In the Hausdorff uniformity, the powerset of a closed set is closed. -/
theorem _root_.IsClosed.powerset_hausdorff {F : Set Œ±} (hF : IsClosed F) :
    IsClosed F.powerset := by
  simp_rw [Set.powerset, ‚Üê isOpen_compl_iff, Set.compl_setOf, ‚Üê Set.inter_compl_nonempty_iff]
  exact isOpen_inter_nonempty_of_isOpen hF.isOpen_compl

@[deprecated (since := "2025-11-23")] alias isClosed_powerset := IsClosed.powerset_hausdorff

theorem isClopen_singleton_empty : IsClopen {(‚àÖ : Set Œ±)} := by
  constructor
  ¬∑ rw [‚Üê Set.powerset_empty]
    exact isClosed_empty.powerset_hausdorff
  ¬∑ simp_rw [isOpen_iff_mem_nhds, Set.mem_singleton_iff, forall_eq, nhds_eq_uniformity]
    filter_upwards [Filter.mem_lift' <| Filter.mem_lift' Filter.univ_mem] with F ‚ü®_, hF‚ü©
    simpa using hF

theorem isUniformEmbedding_singleton : IsUniformEmbedding ({¬∑} : Œ± ‚Üí Set Œ±) where
  injective := Set.singleton_injective
  comap_uniformity := by
    change Filter.comap _ (Filter.lift' _ _) = _
    simp_rw [Filter.comap_lift'_eq, Function.comp_def, Set.preimage,
      singleton_mem_hausdorffEntourage]
    exact Filter.lift'_id

theorem isClosedEmbedding_singleton [T0Space Œ±] :
    Topology.IsClosedEmbedding ({¬∑} : Œ± ‚Üí Set Œ±) where
  __ := isUniformEmbedding_singleton.isEmbedding
  isClosed_range := by
    rw [‚Üê isOpen_compl_iff, isOpen_iff_mem_nhds]
    intro s hs
    rcases Set.eq_empty_or_nonempty s with rfl | h
    ¬∑ rwa [(isOpen_singleton_iff_nhds_eq_pure _).mp isClopen_singleton_empty.isOpen,
        Filter.mem_pure]
    rcases h.exists_eq_singleton_or_nontrivial with ‚ü®x, rfl‚ü© | ‚ü®x, hx, y, hy, hxy‚ü©
    ¬∑ cases hs <| Set.mem_range_self x
    obtain ‚ü®U, V, hU, hV, hxU, hyV, hUV‚ü© := t2_separation hxy
    filter_upwards [(isOpen_inter_nonempty_of_isOpen hU).inter (isOpen_inter_nonempty_of_isOpen hV)
      |>.mem_nhds ‚ü®‚ü®x, hx, hxU‚ü©, ‚ü®y, hy, hyV‚ü©‚ü©]
    rintro _ ‚ü®hzU, hzV‚ü© ‚ü®z, rfl‚ü©
    rw [Set.mem_setOf, Set.singleton_inter_nonempty] at hzU hzV
    exact hUV.notMem_of_mem_left hzU hzV

theorem uniformContinuous_union : UniformContinuous (fun x : Set Œ± √ó Set Œ± => x.1 ‚à™ x.2) := by
  refine Filter.tendsto_lift'.mpr fun U hU => ?_
  filter_upwards [entourageProd_mem_uniformity (Filter.mem_lift' hU) (Filter.mem_lift' hU)]
    with _ ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© using union_mem_hausdorffEntourage U h‚ÇÅ h‚ÇÇ

theorem uniformContinuous_closure : UniformContinuous (closure (X := Œ±)) := by
  simp_rw [UniformContinuous, (ùì§ Œ±).basis_sets.uniformity_hausdorff.tendsto_iff
    (ùì§ Œ±).basis_sets.uniformity_hausdorff, Function.comp_id, mem_hausdorffEntourage]
  intro U hU
  obtain ‚ü®V : SetRel Œ± Œ±, hV, hVU‚ü© := comp_mem_uniformity_sets hU
  refine ‚ü®V, hV, fun ‚ü®s, t‚ü© ‚ü®hst, hts‚ü© => ?_‚ü©
  simp only at *
  constructor
  ¬∑ grw [closure_subset_preimage hV s, hst, ‚Üê subset_closure, ‚Üê hVU, SetRel.preimage_comp]
  ¬∑ grw [closure_subset_image hV t, hts, ‚Üê subset_closure, ‚Üê hVU, SetRel.image_comp]

@[fun_prop]
theorem continuous_closure : Continuous (closure (X := Œ±)) :=
  uniformContinuous_closure.continuous

theorem isUniformInducing_closure : IsUniformInducing (closure (X := Œ±)) := by
  refine ‚ü®le_antisymm ?_ <| Filter.map_le_iff_le_comap.mp uniformContinuous_closure‚ü©
  rw [(ùì§ Œ±).basis_sets.uniformity_hausdorff.comap _ |>.le_basis_iff
    (ùì§ Œ±).basis_sets.uniformity_hausdorff, Function.comp_id]
  intro U hU
  obtain ‚ü®V : SetRel Œ± Œ±, hV, hVU‚ü© := comp_mem_uniformity_sets hU
  refine ‚ü®V, hV, fun ‚ü®s, t‚ü© ‚ü®hst, hts‚ü© => ?_‚ü©
  simp only [mem_hausdorffEntourage] at *
  constructor
  ¬∑ grw [subset_closure (s := s), hst, closure_subset_preimage hV t, ‚Üê hVU, SetRel.preimage_comp]
  ¬∑ grw [subset_closure (s := t), hts, closure_subset_image hV s, ‚Üê hVU, SetRel.image_comp]

theorem nhds_closure (s : Set Œ±) : ùìù (closure s) = ùìù s := by
  simp_rw +singlePass [isUniformInducing_closure.isInducing.nhds_eq_comap, closure_closure]

end UniformSpace.hausdorff

/-- When `Set` is equipped with the Hausdorff uniformity, taking the image under a uniformly
continuous map is uniformly continuous. -/
theorem UniformContinuous.image_hausdorff {f : Œ± ‚Üí Œ≤} (hf : UniformContinuous f) :
    UniformContinuous (f '' ¬∑) := by
  refine Filter.tendsto_lift'.mpr fun U hU => ?_
  filter_upwards [Filter.mem_lift' (hf hU)] with ‚ü®s, t‚ü© ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©
  simp_rw [mem_hausdorffEntourage, Set.image_subset_iff]
  exact ‚ü®h‚ÇÅ.trans fun x ‚ü®y, hy, hxy‚ü© => ‚ü®f y, Set.mem_image_of_mem f hy, hxy‚ü©,
    h‚ÇÇ.trans fun x ‚ü®y, hy, hxy‚ü© => ‚ü®f y, Set.mem_image_of_mem f hy, hxy‚ü©‚ü©

/-- When `Set` is equipped with the Hausdorff uniformity, taking the image under a uniform
inducing map is uniform inducing. -/
theorem IsUniformInducing.image_hausdorff {f : Œ± ‚Üí Œ≤} (hf : IsUniformInducing f) :
    IsUniformInducing (f '' ¬∑) := by
  constructor
  change Filter.comap _ (Filter.lift' _ _) = Filter.lift' _ _
  rw [Filter.comap_lift'_eq, ‚Üê hf.comap_uniformity,
    Filter.comap_lift'_eq2 monotone_hausdorffEntourage]
  congr with U ‚ü®s, t‚ü©
  simp only [Function.comp, hausdorffEntourage, SetRel.preimage, SetRel.image, Set.preimage,
    Set.mem_setOf, Set.image_subset_iff, Set.exists_mem_image]

/-- When `Set` is equipped with the Hausdorff uniformity, taking the image under a uniform
embedding is a uniform embedding. -/
theorem IsUniformEmbedding.image_hausdorff {f : Œ± ‚Üí Œ≤} (hf : IsUniformEmbedding f) :
    IsUniformEmbedding (f '' ¬∑) where
  __ := hf.isUniformInducing.image_hausdorff
  injective := hf.injective.image_injective

/-- In the Hausdorff uniformity, the powerset of a totally bounded set is totally bounded. -/
theorem TotallyBounded.powerset_hausdorff {t : Set Œ±} (ht : TotallyBounded t) :
    TotallyBounded t.powerset := by
  simp_rw [(ùì§ Œ±).basis_sets.uniformity_hausdorff.totallyBounded_iff, Function.comp_id,
    Set.powerset, Set.setOf_subset, Set.mem_iUnion]
  intro (U : SetRel Œ± Œ±) hU
  obtain ‚ü®u, hu, ht‚ü© := ht U hU
  refine ‚ü®u.powerset, hu.powerset, fun s hs => ‚ü®u ‚à© U.image s, by grind, fun x hx => ?_,
    fun x ‚ü®_, hx‚ü© => hx‚ü©‚ü©
  obtain ‚ü®y, hy, hxy‚ü© := Set.mem_iUnion‚ÇÇ.mp (ht (hs hx))
  exact ‚ü®y, ‚ü®hy, ‚ü®x, hx, hxy‚ü©‚ü©, hxy‚ü©

/-- The neighborhoods of a totally bounded set in the Hausdorff uniformity are neighborhoods in the
Vietoris topology. -/
theorem TotallyBounded.nhds_vietoris_le_nhds_hausdorff {s : Set Œ±} (hs : TotallyBounded s) :
    @nhds _ (.vietoris Œ±) s ‚â§ ùìù s := by
  open UniformSpace TopologicalSpace.vietoris in
  simp_rw [nhds_eq_comap_uniformity,
    uniformity_hasBasis_open.uniformity_hausdorff |>.comap _ |>.ge_iff, Function.comp_id,
    hausdorffEntourage, Set.preimage_setOf_eq, Set.setOf_and]
  intro U ‚ü®hU‚ÇÅ, hU‚ÇÇ‚ü©
  have : U.IsRefl := ‚ü®fun _ => refl_mem_uniformity hU‚ÇÅ‚ü©
  let := TopologicalSpace.vietoris Œ±
  refine Filter.inter_mem ?_ <| hU‚ÇÇ.relImage.powerset_vietoris.mem_nhds <|
    SetRel.self_subset_image _
  obtain ‚ü®V : SetRel Œ± Œ±, hV‚ÇÅ, hV‚ÇÇ, _, hVU‚ü© := comp_open_symm_mem_uniformity_sets hU‚ÇÅ
  obtain ‚ü®t, ht‚ÇÅ, ht‚ÇÇ‚ü© := hs.exists_prodMk_finset_mem_hausdorffEntourage hV‚ÇÅ
  dsimp only at ht‚ÇÅ ht‚ÇÇ
  filter_upwards [(Filter.eventually_all_finset t).mpr fun x hx =>
    isOpen_inter_nonempty_of_isOpen (isOpen_ball x hV‚ÇÇ) |>.eventually_mem (ht‚ÇÅ hx)]
    with u (hu : ‚Üët ‚äÜ V.preimage ‚Üëu)
  grw [ht‚ÇÇ, ‚Üê SetRel.preimage_eq_image, hu, ‚Üê hVU, SetRel.preimage_comp]

/-- A compact set has the same neighborhoods in the Hausdorff uniformity and the Vietoris topology.
-/
theorem IsCompact.nhds_hausdorff_eq_nhds_vietoris {s : Set Œ±} (hs : IsCompact s) :
    ùìù s = @nhds _ (.vietoris Œ±) s := by
  refine le_antisymm ?_ hs.totallyBounded.nhds_vietoris_le_nhds_hausdorff
  simp_rw [TopologicalSpace.nhds_generateFrom, le_iInf‚ÇÇ_iff, Filter.le_principal_iff]
  rintro _ ‚ü®hs', (‚ü®U, hU, rfl‚ü© | ‚ü®U, hU, rfl‚ü©)‚ü©
  ¬∑ obtain ‚ü®V : SetRel Œ± Œ±, hV‚ÇÅ, hV‚ÇÇ‚ü© :=
      hs.nhdsSet_basis_uniformity (ùì§ Œ±).basis_sets |>.mem_iff.mp (hU.mem_nhdsSet.mpr hs')
    filter_upwards [UniformSpace.ball_mem_nhds _ (Filter.mem_lift' hV‚ÇÅ)]
      with t ‚ü®_, ht‚ü©
    exact ht.trans fun x ‚ü®y, hy, hxy‚ü© => hV‚ÇÇ <| Set.mem_biUnion hy hxy
  ¬∑ exact (UniformSpace.hausdorff.isOpen_inter_nonempty_of_isOpen hU).mem_nhds hs'

namespace TopologicalSpace.Closeds

instance uniformSpace : UniformSpace (Closeds Œ±) :=
  .comap (‚Üë) (.hausdorff Œ±)

theorem uniformity_def :
    ùì§ (Closeds Œ±) = .comap (Prod.map (‚Üë) (‚Üë)) ((ùì§ Œ±).lift' hausdorffEntourage) :=
  rfl

theorem _root_.Filter.HasBasis.uniformity_closeds
    {Œπ : Sort*} {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí Set (Œ± √ó Œ±)} (h : (ùì§ Œ±).HasBasis p s) :
    (ùì§ (Closeds Œ±)).HasBasis p (fun i => Prod.map (‚Üë) (‚Üë) ‚Åª¬π' (hausdorffEntourage (s i))) :=
  h.uniformity_hausdorff.comap _

theorem isUniformEmbedding_coe : IsUniformEmbedding ((‚Üë) : Closeds Œ± ‚Üí Set Œ±) where
  injective := SetLike.coe_injective
  comap_uniformity := rfl

theorem uniformContinuous_coe : UniformContinuous ((‚Üë) : Closeds Œ± ‚Üí Set Œ±) :=
  isUniformEmbedding_coe.uniformContinuous

theorem isOpen_inter_nonempty_of_isOpen {s : Set Œ±} (hs : IsOpen s) :
    IsOpen {t : Closeds Œ± | ((t : Set Œ±) ‚à© s).Nonempty} :=
  isOpen_induced (UniformSpace.hausdorff.isOpen_inter_nonempty_of_isOpen hs)

theorem isClosed_subsets_of_isClosed {s : Set Œ±} (hs : IsClosed s) :
    IsClosed {t : Closeds Œ± | (t : Set Œ±) ‚äÜ s} :=
  isClosed_induced hs.powerset_hausdorff

theorem totallyBounded_subsets_of_totallyBounded {t : Set Œ±} (ht : TotallyBounded t) :
    TotallyBounded {F : Closeds Œ± | ‚ÜëF ‚äÜ t} :=
  totallyBounded_preimage isUniformEmbedding_coe.isUniformInducing ht.powerset_hausdorff

section T0Space

variable [T0Space Œ±]

theorem isUniformEmbedding_singleton : IsUniformEmbedding ({¬∑} : Œ± ‚Üí Closeds Œ±) :=
  isUniformEmbedding_coe.of_comp_iff.mp UniformSpace.hausdorff.isUniformEmbedding_singleton

theorem uniformContinuous_singleton : UniformContinuous ({¬∑} : Œ± ‚Üí Closeds Œ±) :=
  isUniformEmbedding_singleton.uniformContinuous

@[fun_prop]
theorem isEmbedding_singleton : IsEmbedding ({¬∑} : Œ± ‚Üí Closeds Œ±) :=
  isUniformEmbedding_singleton.isEmbedding

@[fun_prop]
theorem continuous_singleton : Continuous ({¬∑} : Œ± ‚Üí Closeds Œ±) :=
  isEmbedding_singleton.continuous

@[fun_prop]
theorem isClosedEmbedding_singleton : Topology.IsClosedEmbedding ({¬∑} : Œ± ‚Üí Closeds Œ±) where
  __ := isUniformEmbedding_singleton.isEmbedding
  isClosed_range := by
    rw [‚Üê SetLike.coe_injective.preimage_image (s := Set.range ({¬∑})), ‚Üê Set.range_comp]
    exact UniformSpace.hausdorff.isClosedEmbedding_singleton.isClosed_range.preimage
      uniformContinuous_coe.continuous

end T0Space

theorem uniformContinuous_sup : UniformContinuous (fun x : Closeds Œ± √ó Closeds Œ± => x.1 ‚äî x.2) :=
  isUniformEmbedding_coe.uniformContinuous_iff.mpr <|
    UniformSpace.hausdorff.uniformContinuous_union.comp <|
      uniformContinuous_coe.prodMap uniformContinuous_coe

theorem _root_.UniformContinuous.sup_closeds
    {f g : Œ± ‚Üí Closeds Œ≤} (hf : UniformContinuous f) (hg : UniformContinuous g) :
    UniformContinuous (fun x => f x ‚äî g x) :=
  uniformContinuous_sup.comp <| hf.prodMk hg

instance : ContinuousSup (Closeds Œ±) :=
  ‚ü®uniformContinuous_sup.continuous‚ü©

instance : T0Space (Closeds Œ±) := by
  suffices ‚àÄ F‚ÇÅ F‚ÇÇ : Closeds Œ±, Inseparable F‚ÇÅ F‚ÇÇ ‚Üí F‚ÇÅ ‚â§ F‚ÇÇ from
    ‚ü®fun F‚ÇÅ F‚ÇÇ h => le_antisymm (this F‚ÇÅ F‚ÇÇ h) (this F‚ÇÇ F‚ÇÅ h.symm)‚ü©
  refine fun F‚ÇÅ F‚ÇÇ h x hx‚ÇÅ => isClosed_iff_frequently.mp F‚ÇÇ.isClosed _ ?_
  rw [nhds_eq_comap_uniformity, Filter.frequently_comap, Filter.frequently_iff]
  intro (U : SetRel Œ± Œ±) hU
  obtain ‚ü®h : (F‚ÇÅ : Set Œ±) ‚äÜ U.preimage F‚ÇÇ, -‚ü© :=
    mem_of_mem_nhds <| h.nhds_le_uniformity <| Filter.preimage_mem_comap <| Filter.mem_lift' hU
  obtain ‚ü®y, hy, hxy‚ü© := h hx‚ÇÅ
  exact ‚ü®(x, y), hxy, y, rfl, hy‚ü©

theorem isUniformInducing_closure : IsUniformInducing (Closeds.closure (Œ± := Œ±)) :=
  isUniformEmbedding_coe.isUniformInducing_comp_iff.mp
    UniformSpace.hausdorff.isUniformInducing_closure

theorem uniformContinuous_closure : UniformContinuous (Closeds.closure (Œ± := Œ±)) :=
  isUniformInducing_closure.uniformContinuous

@[fun_prop]
theorem continuous_closure : Continuous (Closeds.closure (Œ± := Œ±)) :=
  uniformContinuous_closure.continuous

end TopologicalSpace.Closeds

namespace TopologicalSpace.Compacts

instance uniformSpace : UniformSpace (Compacts Œ±) :=
  .replaceTopology (.comap (‚Üë) (.hausdorff Œ±)) <| ext_nhds fun K =>  by
    simp_rw [nhds_induced, K.isCompact.nhds_hausdorff_eq_nhds_vietoris]

theorem uniformity_def :
    ùì§ (Compacts Œ±) = .comap (Prod.map (‚Üë) (‚Üë)) ((ùì§ Œ±).lift' hausdorffEntourage) :=
  rfl

theorem _root_.Filter.HasBasis.uniformity_compacts
    {Œπ : Sort*} {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí Set (Œ± √ó Œ±)} (h : (ùì§ Œ±).HasBasis p s) :
    (ùì§ (Compacts Œ±)).HasBasis p (fun i => Prod.map (‚Üë) (‚Üë) ‚Åª¬π' (hausdorffEntourage (s i))) :=
  h.uniformity_hausdorff.comap _

theorem isUniformEmbedding_coe : IsUniformEmbedding ((‚Üë) : Compacts Œ± ‚Üí Set Œ±) where
  injective := SetLike.coe_injective
  comap_uniformity := rfl

theorem uniformContinuous_coe : UniformContinuous ((‚Üë) : Compacts Œ± ‚Üí Set Œ±) :=
  isUniformEmbedding_coe.uniformContinuous

theorem isUniformEmbedding_toCloseds [T2Space Œ±] : IsUniformEmbedding (toCloseds (Œ± := Œ±)) where
  injective := toCloseds_injective
  comap_uniformity := Filter.comap_comap

theorem uniformContinuous_toCloseds [T2Space Œ±] : UniformContinuous (toCloseds (Œ± := Œ±)) :=
  isUniformEmbedding_toCloseds.uniformContinuous

@[fun_prop]
theorem isEmbedding_toCloseds [T2Space Œ±] : IsEmbedding (toCloseds (Œ± := Œ±)) :=
  isUniformEmbedding_toCloseds.isEmbedding

@[fun_prop]
theorem continuous_toCloseds [T2Space Œ±] : Continuous (toCloseds (Œ± := Œ±)) :=
  uniformContinuous_toCloseds.continuous

theorem totallyBounded_subsets_of_totallyBounded {t : Set Œ±} (ht : TotallyBounded t) :
    TotallyBounded {K : Compacts Œ± | ‚ÜëK ‚äÜ t} :=
  totallyBounded_preimage isUniformEmbedding_coe.isUniformInducing ht.powerset_hausdorff

theorem isUniformEmbedding_singleton : IsUniformEmbedding ({¬∑} : Œ± ‚Üí Compacts Œ±) :=
  isUniformEmbedding_coe.of_comp_iff.mp UniformSpace.hausdorff.isUniformEmbedding_singleton

theorem uniformContinuous_singleton : UniformContinuous ({¬∑} : Œ± ‚Üí Compacts Œ±) :=
  isUniformEmbedding_singleton.uniformContinuous

theorem uniformContinuous_sup :
    UniformContinuous (fun x : Compacts Œ± √ó Compacts Œ± => x.1 ‚äî x.2) :=
  isUniformEmbedding_coe.uniformContinuous_iff.mpr <|
    UniformSpace.hausdorff.uniformContinuous_union.comp <|
      uniformContinuous_coe.prodMap uniformContinuous_coe

theorem _root_.UniformContinuous.sup_compacts
    {f g : Œ± ‚Üí Compacts Œ≤} (hf : UniformContinuous f) (hg : UniformContinuous g) :
    UniformContinuous (fun x => f x ‚äî g x) :=
  uniformContinuous_sup.comp <| hf.prodMk hg

theorem _root_.UniformContinuous.compacts_map {f : Œ± ‚Üí Œ≤} (hf : UniformContinuous f) :
    UniformContinuous (Compacts.map f hf.continuous) :=
  isUniformEmbedding_coe.uniformContinuous_iff.mpr <| hf.image_hausdorff.comp uniformContinuous_coe

theorem _root_.IsUniformInducing.compacts_map {f : Œ± ‚Üí Œ≤} (hf : IsUniformInducing f) :
    IsUniformInducing (Compacts.map f hf.uniformContinuous.continuous) :=
  .of_comp hf.uniformContinuous.compacts_map uniformContinuous_coe <|
    hf.image_hausdorff.comp isUniformEmbedding_coe.isUniformInducing

theorem _root_.IsUniformEmbedding.compacts_map {f : Œ± ‚Üí Œ≤} (hf : IsUniformEmbedding f) :
    IsUniformEmbedding (Compacts.map f hf.uniformContinuous.continuous) where
  __ := hf.isUniformInducing.compacts_map
  injective := map_injective hf.uniformContinuous.continuous hf.injective

end TopologicalSpace.Compacts

namespace TopologicalSpace.NonemptyCompacts

instance uniformSpace : UniformSpace (NonemptyCompacts Œ±) :=
  .replaceTopology (.comap (‚Üë) (.hausdorff Œ±)) <| ext_nhds fun K =>  by
    simp_rw [nhds_induced, K.isCompact.nhds_hausdorff_eq_nhds_vietoris]

theorem uniformity_def :
    ùì§ (NonemptyCompacts Œ±) = .comap (Prod.map (‚Üë) (‚Üë)) ((ùì§ Œ±).lift' hausdorffEntourage) :=
  rfl

theorem _root_.Filter.HasBasis.uniformity_nonemptyCompacts
    {Œπ : Sort*} {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí Set (Œ± √ó Œ±)} (h : (ùì§ Œ±).HasBasis p s) :
    (ùì§ (NonemptyCompacts Œ±)).HasBasis p
      (fun i => Prod.map (‚Üë) (‚Üë) ‚Åª¬π' (hausdorffEntourage (s i))) :=
  h.uniformity_hausdorff.comap _

theorem isUniformEmbedding_coe : IsUniformEmbedding ((‚Üë) : NonemptyCompacts Œ± ‚Üí Set Œ±) where
  injective := SetLike.coe_injective
  comap_uniformity := rfl

theorem uniformContinuous_coe : UniformContinuous ((‚Üë) : NonemptyCompacts Œ± ‚Üí Set Œ±) :=
  isUniformEmbedding_coe.uniformContinuous

theorem isUniformEmbedding_toCloseds [T2Space Œ±] : IsUniformEmbedding (toCloseds (Œ± := Œ±)) where
  injective := toCloseds_injective
  comap_uniformity := Filter.comap_comap

theorem uniformContinuous_toCloseds [T2Space Œ±] : UniformContinuous (toCloseds (Œ± := Œ±)) :=
  isUniformEmbedding_toCloseds.uniformContinuous

@[fun_prop]
theorem isEmbedding_toCloseds [T2Space Œ±] : IsEmbedding (toCloseds (Œ± := Œ±)) :=
  isUniformEmbedding_toCloseds.isEmbedding

@[fun_prop]
theorem continuous_toCloseds [T2Space Œ±] : Continuous (toCloseds (Œ± := Œ±)) :=
  uniformContinuous_toCloseds.continuous

theorem isUniformEmbedding_toCompacts : IsUniformEmbedding (toCompacts (Œ± := Œ±)) where
  injective := toCompacts_injective
  comap_uniformity := Filter.comap_comap

theorem uniformContinuous_toCompacts : UniformContinuous (toCompacts (Œ± := Œ±)) :=
  isUniformEmbedding_toCompacts.uniformContinuous

theorem totallyBounded_subsets_of_totallyBounded {t : Set Œ±} (ht : TotallyBounded t) :
    TotallyBounded {K : NonemptyCompacts Œ± | ‚ÜëK ‚äÜ t} :=
  totallyBounded_preimage isUniformEmbedding_coe.isUniformInducing ht.powerset_hausdorff

theorem isUniformEmbedding_singleton : IsUniformEmbedding ({¬∑} : Œ± ‚Üí NonemptyCompacts Œ±) :=
  isUniformEmbedding_coe.of_comp_iff.mp UniformSpace.hausdorff.isUniformEmbedding_singleton

theorem uniformContinuous_singleton : UniformContinuous ({¬∑} : Œ± ‚Üí NonemptyCompacts Œ±) :=
  isUniformEmbedding_singleton.uniformContinuous

theorem uniformContinuous_sup :
    UniformContinuous (fun x : NonemptyCompacts Œ± √ó NonemptyCompacts Œ± => x.1 ‚äî x.2) :=
  isUniformEmbedding_coe.uniformContinuous_iff.mpr <|
    UniformSpace.hausdorff.uniformContinuous_union.comp <|
      uniformContinuous_coe.prodMap uniformContinuous_coe

theorem _root_.UniformContinuous.sup_nonemptyCompacts
    {f g : Œ± ‚Üí NonemptyCompacts Œ≤} (hf : UniformContinuous f) (hg : UniformContinuous g) :
    UniformContinuous (fun x => f x ‚äî g x) :=
  uniformContinuous_sup.comp <| hf.prodMk hg

theorem _root_.UniformContinuous.nonemptyCompacts_map {f : Œ± ‚Üí Œ≤} (hf : UniformContinuous f) :
    UniformContinuous (NonemptyCompacts.map f hf.continuous) :=
  isUniformEmbedding_coe.uniformContinuous_iff.mpr <| hf.image_hausdorff.comp uniformContinuous_coe

theorem _root_.IsUniformInducing.nonemptyCompacts_map {f : Œ± ‚Üí Œ≤} (hf : IsUniformInducing f) :
    IsUniformInducing (NonemptyCompacts.map f hf.uniformContinuous.continuous) :=
  .of_comp hf.uniformContinuous.nonemptyCompacts_map uniformContinuous_coe <|
    hf.image_hausdorff.comp isUniformEmbedding_coe.isUniformInducing

theorem _root_.IsUniformEmbedding.nonemptyCompacts_map {f : Œ± ‚Üí Œ≤} (hf : IsUniformEmbedding f) :
    IsUniformEmbedding (NonemptyCompacts.map f hf.uniformContinuous.continuous) where
  __ := hf.isUniformInducing.nonemptyCompacts_map
  injective := map_injective hf.uniformContinuous.continuous hf.injective

end TopologicalSpace.NonemptyCompacts
