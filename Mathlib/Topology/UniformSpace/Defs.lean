/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Patrick Massot
-/
import Mathlib.Algebra.Group.Defs
import Mathlib.Order.Filter.Tendsto
import Mathlib.Tactic.Monotonicity.Basic
import Mathlib.Topology.Order

/-!
# Uniform spaces

Uniform spaces are a generalization of metric spaces and topological groups. Many concepts directly
generalize to uniform spaces, e.g.

* uniform continuity (in this file)
* completeness (in `Cauchy.lean`)
* extension of uniform continuous functions to complete spaces (in `IsUniformEmbedding.lean`)
* totally bounded sets (in `Cauchy.lean`)
* totally bounded complete sets are compact (in `Cauchy.lean`)

A uniform structure on a type `X` is a filter `ğ“¤ X` on `X Ã— X` satisfying some conditions
which makes it reasonable to say that `âˆ€á¶  (p : X Ã— X) in ğ“¤ X, ...` means
"for all p.1 and p.2 in X close enough, ...". Elements of this filter are called entourages
of `X`. The two main examples are:

* If `X` is a metric space, `V âˆˆ ğ“¤ X â†” âˆƒ Îµ > 0, { p | dist p.1 p.2 < Îµ } âŠ† V`
* If `G` is an additive topological group, `V âˆˆ ğ“¤ G â†” âˆƒ U âˆˆ ğ“ (0 : G), {p | p.2 - p.1 âˆˆ U} âŠ† V`

Those examples are generalizations in two different directions of the elementary example where
`X = â„` and `V âˆˆ ğ“¤ â„ â†” âˆƒ Îµ > 0, { p | |p.2 - p.1| < Îµ } âŠ† V` which features both the topological
group structure on `â„` and its metric space structure.

Each uniform structure on `X` induces a topology on `X` characterized by

> `nhds_eq_comap_uniformity : âˆ€ {x : X}, ğ“ x = comap (Prod.mk x) (ğ“¤ X)`

where `Prod.mk x : X â†’ X Ã— X := (fun y â†¦ (x, y))` is the partial evaluation of the product
constructor.

The dictionary with metric spaces includes:
* an upper bound for `dist x y` translates into `(x, y) âˆˆ V` for some `V âˆˆ ğ“¤ X`
* a ball `ball x r` roughly corresponds to `UniformSpace.ball x V := {y | (x, y) âˆˆ V}`
  for some `V âˆˆ ğ“¤ X`, but the later is more general (it includes in
  particular both open and closed balls for suitable `V`).
  In particular we have:
  `isOpen_iff_ball_subset {s : Set X} : IsOpen s â†” âˆ€ x âˆˆ s, âˆƒ V âˆˆ ğ“¤ X, ball x V âŠ† s`

The triangle inequality is abstracted to a statement involving the composition of relations in `X`.
First note that the triangle inequality in a metric space is equivalent to
`âˆ€ (x y z : X) (r r' : â„), dist x y â‰¤ r â†’ dist y z â‰¤ r' â†’ dist x z â‰¤ r + r'`.
Then, for any `V` and `W` with type `Set (X Ã— X)`, the composition `V â—‹ W : Set (X Ã— X)` is
defined as `{ p : X Ã— X | âˆƒ z, (p.1, z) âˆˆ V âˆ§ (z, p.2) âˆˆ W }`.
In the metric space case, if `V = { p | dist p.1 p.2 â‰¤ r }` and `W = { p | dist p.1 p.2 â‰¤ r' }`
then the triangle inequality, as reformulated above, says `V â—‹ W` is contained in
`{p | dist p.1 p.2 â‰¤ r + r'}` which is the entourage associated to the radius `r + r'`.
In general we have `mem_ball_comp (h : y âˆˆ ball x V) (h' : z âˆˆ ball y W) : z âˆˆ ball x (V â—‹ W)`.
Note that this discussion does not depend on any axiom imposed on the uniformity filter,
it is simply captured by the definition of composition.

The uniform space axioms ask the filter `ğ“¤ X` to satisfy the following:
* every `V âˆˆ ğ“¤ X` contains the diagonal `idRel = { p | p.1 = p.2 }`. This abstracts the fact
  that `dist x x â‰¤ r` for every non-negative radius `r` in the metric space case and also that
  `x - x` belongs to every neighborhood of zero in the topological group case.
* `V âˆˆ ğ“¤ X â†’ Prod.swap '' V âˆˆ ğ“¤ X`. This is tightly related the fact that `dist x y = dist y x`
  in a metric space, and to continuity of negation in the topological group case.
* `âˆ€ V âˆˆ ğ“¤ X, âˆƒ W âˆˆ ğ“¤ X, W â—‹ W âŠ† V`. In the metric space case, it corresponds
  to cutting the radius of a ball in half and applying the triangle inequality.
  In the topological group case, it comes from continuity of addition at `(0, 0)`.

These three axioms are stated more abstractly in the definition below, in terms of
operations on filters, without directly manipulating entourages.

## Main definitions

* `UniformSpace X` is a uniform space structure on a type `X`
* `UniformContinuous f` is a predicate saying a function `f : Î± â†’ Î²` between uniform spaces
  is uniformly continuous : `âˆ€ r âˆˆ ğ“¤ Î², âˆ€á¶  (x : Î± Ã— Î±) in ğ“¤ Î±, (f x.1, f x.2) âˆˆ r`

## Notations

Localized in `Uniformity`, we have the notation `ğ“¤ X` for the uniformity on a uniform space `X`,
and `â—‹` for composition of relations, seen as terms with type `Set (X Ã— X)`.

## Implementation notes

There is already a theory of relations in `Data/Rel.lean` where the main definition is
`def Rel (Î± Î² : Type*) := Î± â†’ Î² â†’ Prop`.
The relations used in the current file involve only one type, but this is not the reason why
we don't reuse `Data/Rel.lean`. We use `Set (Î± Ã— Î±)`
instead of `Rel Î± Î±` because we really need sets to use the filter library, and elements
of filters on `Î± Ã— Î±` have type `Set (Î± Ã— Î±)`.

The structure `UniformSpace X` bundles a uniform structure on `X`, a topology on `X` and
an assumption saying those are compatible. This may not seem mathematically reasonable at first,
but is in fact an instance of the forgetful inheritance pattern. See Note [forgetful inheritance]
below.

## References

The formalization uses the books:

* [N. Bourbaki, *General Topology*][bourbaki1966]
* [I. M. James, *Topologies and Uniformities*][james1999]

But it makes a more systematic use of the filter library.
-/

open Set Filter Topology

universe u v ua ub uc ud

/-!
### Relations, seen as `Set (Î± Ã— Î±)`
-/

variable {Î± : Type ua} {Î² : Type ub} {Î³ : Type uc} {Î´ : Type ud} {Î¹ : Sort*}

/-- The identity relation, or the graph of the identity function -/
def idRel {Î± : Type*} :=
  { p : Î± Ã— Î± | p.1 = p.2 }

@[simp]
theorem mem_idRel {a b : Î±} : (a, b) âˆˆ @idRel Î± â†” a = b :=
  Iff.rfl

@[simp]
theorem idRel_subset {s : Set (Î± Ã— Î±)} : idRel âŠ† s â†” âˆ€ a, (a, a) âˆˆ s := by
  simp [subset_def]

theorem eq_singleton_left_of_prod_subset_idRel {X : Type*} {S T : Set X} (hS : S.Nonempty)
    (hT : T.Nonempty) (h_diag : S Ã—Ë¢ T âŠ† idRel) : âˆƒ x, S = {x} := by
  rcases hS, hT with âŸ¨âŸ¨s, hsâŸ©, âŸ¨t, htâŸ©âŸ©
  refine âŸ¨s, eq_singleton_iff_nonempty_unique_mem.mpr âŸ¨âŸ¨s, hsâŸ©, fun x hx â†¦ ?_âŸ©âŸ©
  rw [prod_subset_iff] at h_diag
  replace hs := h_diag s hs t ht
  replace hx := h_diag x hx t ht
  simp only [idRel, mem_setOf_eq] at hx hs
  rwa [â† hs] at hx

theorem eq_singleton_right_prod_subset_idRel {X : Type*} {S T : Set X} (hS : S.Nonempty)
    (hT : T.Nonempty) (h_diag : S Ã—Ë¢ T âŠ† idRel) : âˆƒ x, T = {x} := by
  rw [Set.prod_subset_iff] at h_diag
  replace h_diag := fun x hx y hy => (h_diag y hy x hx).symm
  exact eq_singleton_left_of_prod_subset_idRel hT hS (prod_subset_iff.mpr h_diag)

theorem eq_singleton_prod_subset_idRel {X : Type*} {S T : Set X} (hS : S.Nonempty)
    (hT : T.Nonempty) (h_diag : S Ã—Ë¢ T âŠ† idRel) : âˆƒ x, S = {x} âˆ§ T = {x} := by
  obtain âŸ¨âŸ¨x, hxâŸ©, âŸ¨y, hyâŸ©âŸ© := eq_singleton_left_of_prod_subset_idRel hS hT h_diag,
    eq_singleton_right_prod_subset_idRel hS hT h_diag
  refine âŸ¨x, âŸ¨hx, ?_âŸ©âŸ©
  rw [hy, Set.singleton_eq_singleton_iff]
  exact (Set.prod_subset_iff.mp h_diag x (by simp only [hx, Set.mem_singleton]) y
    (by simp only [hy, Set.mem_singleton])).symm

/-- The composition of relations -/
def compRel (râ‚ râ‚‚ : Set (Î± Ã— Î±)) :=
  { p : Î± Ã— Î± | âˆƒ z : Î±, (p.1, z) âˆˆ râ‚ âˆ§ (z, p.2) âˆˆ râ‚‚ }

@[inherit_doc]
scoped[Uniformity] infixl:62 " â—‹ " => compRel
open Uniformity

@[simp]
theorem mem_compRel {Î± : Type u} {râ‚ râ‚‚ : Set (Î± Ã— Î±)} {x y : Î±} :
    (x, y) âˆˆ râ‚ â—‹ râ‚‚ â†” âˆƒ z, (x, z) âˆˆ râ‚ âˆ§ (z, y) âˆˆ râ‚‚ :=
  Iff.rfl

@[simp]
theorem swap_idRel : Prod.swap '' idRel = @idRel Î± :=
  Set.ext fun âŸ¨a, bâŸ© => by simpa [image_swap_eq_preimage_swap] using eq_comm

theorem Monotone.compRel [Preorder Î²] {f g : Î² â†’ Set (Î± Ã— Î±)} (hf : Monotone f) (hg : Monotone g) :
    Monotone fun x => f x â—‹ g x := fun _ _ h _ âŸ¨z, hâ‚, hâ‚‚âŸ© => âŸ¨z, hf h hâ‚, hg h hâ‚‚âŸ©

@[mono, gcongr]
theorem compRel_mono {f g h k : Set (Î± Ã— Î±)} (hâ‚ : f âŠ† h) (hâ‚‚ : g âŠ† k) : f â—‹ g âŠ† h â—‹ k :=
  fun _ âŸ¨z, h, h'âŸ© => âŸ¨z, hâ‚ h, hâ‚‚ h'âŸ©

theorem prodMk_mem_compRel {a b c : Î±} {s t : Set (Î± Ã— Î±)} (hâ‚ : (a, c) âˆˆ s) (hâ‚‚ : (c, b) âˆˆ t) :
    (a, b) âˆˆ s â—‹ t :=
  âŸ¨c, hâ‚, hâ‚‚âŸ©

@[deprecated (since := "2025-03-10")]
alias prod_mk_mem_compRel := prodMk_mem_compRel

@[simp]
theorem id_compRel {r : Set (Î± Ã— Î±)} : idRel â—‹ r = r :=
  Set.ext fun âŸ¨a, bâŸ© => by simp

theorem compRel_assoc {r s t : Set (Î± Ã— Î±)} : r â—‹ s â—‹ t = r â—‹ (s â—‹ t) := by
  ext âŸ¨a, bâŸ©; simp only [mem_compRel]; tauto

theorem left_subset_compRel {s t : Set (Î± Ã— Î±)} (h : idRel âŠ† t) : s âŠ† s â—‹ t := fun âŸ¨_x, yâŸ© xy_in =>
  âŸ¨y, xy_in, h <| rflâŸ©

theorem right_subset_compRel {s t : Set (Î± Ã— Î±)} (h : idRel âŠ† s) : t âŠ† s â—‹ t := fun âŸ¨x, _yâŸ© xy_in =>
  âŸ¨x, h <| rfl, xy_inâŸ©

theorem subset_comp_self {s : Set (Î± Ã— Î±)} (h : idRel âŠ† s) : s âŠ† s â—‹ s :=
  left_subset_compRel h

theorem subset_iterate_compRel {s t : Set (Î± Ã— Î±)} (h : idRel âŠ† s) (n : â„•) :
    t âŠ† (s â—‹ Â·)^[n] t := by
  induction n generalizing t with
  | zero => exact Subset.rfl
  | succ n ihn => exact (right_subset_compRel h).trans ihn

/-- The relation is invariant under swapping factors. -/
def IsSymmetricRel (V : Set (Î± Ã— Î±)) : Prop :=
  Prod.swap â»Â¹' V = V

@[deprecated (since := "2025-03-05")]
alias SymmetricRel := IsSymmetricRel

/-- The maximal symmetric relation contained in a given relation. -/
def symmetrizeRel (V : Set (Î± Ã— Î±)) : Set (Î± Ã— Î±) :=
  V âˆ© Prod.swap â»Â¹' V

theorem symmetric_symmetrizeRel (V : Set (Î± Ã— Î±)) : IsSymmetricRel (symmetrizeRel V) := by
  simp [IsSymmetricRel, symmetrizeRel, preimage_inter, inter_comm, â† preimage_comp]

theorem symmetrizeRel_subset_self (V : Set (Î± Ã— Î±)) : symmetrizeRel V âŠ† V :=
  sep_subset _ _

@[mono]
theorem symmetrize_mono {V W : Set (Î± Ã— Î±)} (h : V âŠ† W) : symmetrizeRel V âŠ† symmetrizeRel W :=
  inter_subset_inter h <| preimage_mono h

theorem IsSymmetricRel.mk_mem_comm {V : Set (Î± Ã— Î±)} (hV : IsSymmetricRel V) {x y : Î±} :
    (x, y) âˆˆ V â†” (y, x) âˆˆ V :=
  Set.ext_iff.1 hV (y, x)

@[deprecated (since := "2025-03-05")]
alias SymmetricRel.mk_mem_comm := IsSymmetricRel.mk_mem_comm

theorem IsSymmetricRel.eq {U : Set (Î± Ã— Î±)} (hU : IsSymmetricRel U) : Prod.swap â»Â¹' U = U :=
  hU

@[deprecated (since := "2025-03-05")]
alias SymmetricRel.eq := IsSymmetricRel.eq

theorem IsSymmetricRel.inter {U V : Set (Î± Ã— Î±)} (hU : IsSymmetricRel U) (hV : IsSymmetricRel V) :
    IsSymmetricRel (U âˆ© V) := by rw [IsSymmetricRel, preimage_inter, hU.eq, hV.eq]

@[deprecated (since := "2025-03-05")]
alias SymmetricRel.inter := IsSymmetricRel.inter

theorem IsSymmetricRel.iInter {U : (i : Î¹) â†’ Set (Î± Ã— Î±)} (hU : âˆ€ i, IsSymmetricRel (U i)) :
    IsSymmetricRel (â‹‚ i, U i) := by
  simp_rw [IsSymmetricRel, preimage_iInter, (hU _).eq]

lemma IsSymmetricRel.sInter {s : Set (Set (Î± Ã— Î±))} (h : âˆ€ i âˆˆ s, IsSymmetricRel i) :
    IsSymmetricRel (â‹‚â‚€ s) := by
  rw [sInter_eq_iInter]
  exact IsSymmetricRel.iInter (by simpa)

lemma isSymmetricRel_idRel : IsSymmetricRel (idRel : Set (Î± Ã— Î±)) := by
  simp [IsSymmetricRel, idRel, eq_comm]

lemma isSymmetricRel_univ : IsSymmetricRel (Set.univ : Set (Î± Ã— Î±)) := by
  simp [IsSymmetricRel]

lemma IsSymmetricRel.preimage_prodMap {U : Set (Î² Ã— Î²)} (ht : IsSymmetricRel U) (f : Î± â†’ Î²) :
    IsSymmetricRel (Prod.map f f â»Â¹' U) :=
  Set.ext fun _ â†¦ ht.mk_mem_comm

/-- This core description of a uniform space is outside of the type class hierarchy. It is useful
  for constructions of uniform spaces, when the topology is derived from the uniform space. -/
structure UniformSpace.Core (Î± : Type u) where
  /-- The uniformity filter. Once `UniformSpace` is defined, `ğ“¤ Î±` (`_root_.uniformity`) becomes the
  normal form. -/
  uniformity : Filter (Î± Ã— Î±)
  /-- Every set in the uniformity filter includes the diagonal. -/
  refl : ğ“Ÿ idRel â‰¤ uniformity
  /-- If `s âˆˆ uniformity`, then `Prod.swap â»Â¹' s âˆˆ uniformity`. -/
  symm : Tendsto Prod.swap uniformity uniformity
  /-- For every set `u âˆˆ uniformity`, there exists `v âˆˆ uniformity` such that `v â—‹ v âŠ† u`. -/
  comp : (uniformity.lift' fun s => s â—‹ s) â‰¤ uniformity

protected theorem UniformSpace.Core.comp_mem_uniformity_sets {c : Core Î±} {s : Set (Î± Ã— Î±)}
    (hs : s âˆˆ c.uniformity) : âˆƒ t âˆˆ c.uniformity, t â—‹ t âŠ† s :=
  (mem_lift'_sets <| monotone_id.compRel monotone_id).mp <| c.comp hs

/-- An alternative constructor for `UniformSpace.Core`. This version unfolds various
`Filter`-related definitions. -/
def UniformSpace.Core.mk' {Î± : Type u} (U : Filter (Î± Ã— Î±)) (refl : âˆ€ r âˆˆ U, âˆ€ (x), (x, x) âˆˆ r)
    (symm : âˆ€ r âˆˆ U, Prod.swap â»Â¹' r âˆˆ U) (comp : âˆ€ r âˆˆ U, âˆƒ t âˆˆ U, t â—‹ t âŠ† r) :
    UniformSpace.Core Î± :=
  âŸ¨U, fun _r ru => idRel_subset.2 (refl _ ru), symm, fun _r ru =>
    let âŸ¨_s, hs, hsrâŸ© := comp _ ru
    mem_of_superset (mem_lift' hs) hsrâŸ©

/-- Defining a `UniformSpace.Core` from a filter basis satisfying some uniformity-like axioms. -/
def UniformSpace.Core.mkOfBasis {Î± : Type u} (B : FilterBasis (Î± Ã— Î±))
    (refl : âˆ€ r âˆˆ B, âˆ€ (x), (x, x) âˆˆ r) (symm : âˆ€ r âˆˆ B, âˆƒ t âˆˆ B, t âŠ† Prod.swap â»Â¹' r)
    (comp : âˆ€ r âˆˆ B, âˆƒ t âˆˆ B, t â—‹ t âŠ† r) : UniformSpace.Core Î± where
  uniformity := B.filter
  refl := B.hasBasis.ge_iff.mpr fun _r ru => idRel_subset.2 <| refl _ ru
  symm := (B.hasBasis.tendsto_iff B.hasBasis).mpr symm
  comp := (HasBasis.le_basis_iff (B.hasBasis.lift' (monotone_id.compRel monotone_id))
    B.hasBasis).2 comp

/-- A uniform space generates a topological space -/
def UniformSpace.Core.toTopologicalSpace {Î± : Type u} (u : UniformSpace.Core Î±) :
    TopologicalSpace Î± :=
  .mkOfNhds fun x â†¦ .comap (Prod.mk x) u.uniformity

theorem UniformSpace.Core.ext :
    âˆ€ {uâ‚ uâ‚‚ : UniformSpace.Core Î±}, uâ‚.uniformity = uâ‚‚.uniformity â†’ uâ‚ = uâ‚‚
  | âŸ¨_, _, _, _âŸ©, âŸ¨_, _, _, _âŸ©, rfl => rfl

theorem UniformSpace.Core.nhds_toTopologicalSpace {Î± : Type u} (u : Core Î±) (x : Î±) :
    @nhds Î± u.toTopologicalSpace x = comap (Prod.mk x) u.uniformity := by
  apply TopologicalSpace.nhds_mkOfNhds_of_hasBasis (fun _ â†¦ (basis_sets _).comap _)
  Â· exact fun a U hU â†¦ u.refl hU rfl
  Â· intro a U hU
    rcases u.comp_mem_uniformity_sets hU with âŸ¨V, hV, hVUâŸ©
    filter_upwards [preimage_mem_comap hV] with b hb
    filter_upwards [preimage_mem_comap hV] with c hc
    exact hVU âŸ¨b, hb, hcâŸ©

-- the topological structure is embedded in the uniform structure
-- to avoid instance diamond issues. See Note [forgetful inheritance].
/-- A uniform space is a generalization of the "uniform" topological aspects of a
  metric space. It consists of a filter on `Î± Ã— Î±` called the "uniformity", which
  satisfies properties analogous to the reflexivity, symmetry, and triangle properties
  of a metric.

  A metric space has a natural uniformity, and a uniform space has a natural topology.
  A topological group also has a natural uniformity, even when it is not metrizable. -/
class UniformSpace (Î± : Type u) extends TopologicalSpace Î± where
  /-- The uniformity filter. -/
  protected uniformity : Filter (Î± Ã— Î±)
  /-- If `s âˆˆ uniformity`, then `Prod.swap â»Â¹' s âˆˆ uniformity`. -/
  protected symm : Tendsto Prod.swap uniformity uniformity
  /-- For every set `u âˆˆ uniformity`, there exists `v âˆˆ uniformity` such that `v â—‹ v âŠ† u`. -/
  protected comp : (uniformity.lift' fun s => s â—‹ s) â‰¤ uniformity
  /-- The uniformity agrees with the topology: the neighborhoods filter of each point `x`
  is equal to `Filter.comap (Prod.mk x) (ğ“¤ Î±)`. -/
  protected nhds_eq_comap_uniformity (x : Î±) : ğ“ x = comap (Prod.mk x) uniformity

/-- The uniformity is a filter on Î± Ã— Î± (inferred from an ambient uniform space
  structure on Î±). -/
def uniformity (Î± : Type u) [UniformSpace Î±] : Filter (Î± Ã— Î±) :=
  @UniformSpace.uniformity Î± _

/-- Notation for the uniformity filter with respect to a non-standard `UniformSpace` instance. -/
scoped[Uniformity] notation "ğ“¤[" u "]" => @uniformity _ u

@[inherit_doc]
scoped[Uniformity] notation "ğ“¤" => uniformity

/-- Construct a `UniformSpace` from a `u : UniformSpace.Core` and a `TopologicalSpace` structure
that is equal to `u.toTopologicalSpace`. -/
abbrev UniformSpace.ofCoreEq {Î± : Type u} (u : UniformSpace.Core Î±) (t : TopologicalSpace Î±)
    (h : t = u.toTopologicalSpace) : UniformSpace Î± where
  __ := u
  toTopologicalSpace := t
  nhds_eq_comap_uniformity x := by rw [h, u.nhds_toTopologicalSpace]

/-- Construct a `UniformSpace` from a `UniformSpace.Core`. -/
abbrev UniformSpace.ofCore {Î± : Type u} (u : UniformSpace.Core Î±) : UniformSpace Î± :=
  .ofCoreEq u _ rfl

/-- Construct a `UniformSpace.Core` from a `UniformSpace`. -/
abbrev UniformSpace.toCore (u : UniformSpace Î±) : UniformSpace.Core Î± where
  __ := u
  refl := by
    rintro U hU âŸ¨x, yâŸ© (rfl : x = y)
    have : Prod.mk x â»Â¹' U âˆˆ ğ“ x := by
      rw [UniformSpace.nhds_eq_comap_uniformity]
      exact preimage_mem_comap hU
    convert mem_of_mem_nhds this

theorem UniformSpace.toCore_toTopologicalSpace (u : UniformSpace Î±) :
    u.toCore.toTopologicalSpace = u.toTopologicalSpace :=
  TopologicalSpace.ext_nhds fun a â†¦ by
    rw [u.nhds_eq_comap_uniformity, u.toCore.nhds_toTopologicalSpace]

lemma UniformSpace.mem_uniformity_ofCore_iff {u : UniformSpace.Core Î±} {s : Set (Î± Ã— Î±)} :
    s âˆˆ ğ“¤[.ofCore u] â†” s âˆˆ u.uniformity :=
  Iff.rfl

@[ext (iff := false)]
protected theorem UniformSpace.ext {uâ‚ uâ‚‚ : UniformSpace Î±} (h : ğ“¤[uâ‚] = ğ“¤[uâ‚‚]) : uâ‚ = uâ‚‚ := by
  have : uâ‚.toTopologicalSpace = uâ‚‚.toTopologicalSpace := TopologicalSpace.ext_nhds fun x â†¦ by
    rw [uâ‚.nhds_eq_comap_uniformity, uâ‚‚.nhds_eq_comap_uniformity]
    exact congr_arg (comap _) h
  cases uâ‚; cases uâ‚‚; congr

protected theorem UniformSpace.ext_iff {uâ‚ uâ‚‚ : UniformSpace Î±} :
    uâ‚ = uâ‚‚ â†” âˆ€ s, s âˆˆ ğ“¤[uâ‚] â†” s âˆˆ ğ“¤[uâ‚‚] :=
  âŸ¨fun h _ => h â–¸ Iff.rfl, fun h => by ext; exact h _âŸ©

theorem UniformSpace.ofCoreEq_toCore (u : UniformSpace Î±) (t : TopologicalSpace Î±)
    (h : t = u.toCore.toTopologicalSpace) : .ofCoreEq u.toCore t h = u :=
  UniformSpace.ext rfl

/-- Replace topology in a `UniformSpace` instance with a propositionally (but possibly not
definitionally) equal one. -/
abbrev UniformSpace.replaceTopology {Î± : Type*} [i : TopologicalSpace Î±] (u : UniformSpace Î±)
    (h : i = u.toTopologicalSpace) : UniformSpace Î± where
  __ := u
  toTopologicalSpace := i
  nhds_eq_comap_uniformity x := by rw [h, u.nhds_eq_comap_uniformity]

theorem UniformSpace.replaceTopology_eq {Î± : Type*} [i : TopologicalSpace Î±] (u : UniformSpace Î±)
    (h : i = u.toTopologicalSpace) : u.replaceTopology h = u :=
  UniformSpace.ext rfl

section UniformSpace

variable [UniformSpace Î±]

theorem nhds_eq_comap_uniformity {x : Î±} : ğ“ x = (ğ“¤ Î±).comap (Prod.mk x) :=
  UniformSpace.nhds_eq_comap_uniformity x

theorem isOpen_uniformity {s : Set Î±} :
    IsOpen s â†” âˆ€ x âˆˆ s, { p : Î± Ã— Î± | p.1 = x â†’ p.2 âˆˆ s } âˆˆ ğ“¤ Î± := by
  simp only [isOpen_iff_mem_nhds, nhds_eq_comap_uniformity, mem_comap_prodMk]

theorem refl_le_uniformity : ğ“Ÿ idRel â‰¤ ğ“¤ Î± :=
  (@UniformSpace.toCore Î± _).refl

instance uniformity.neBot [Nonempty Î±] : NeBot (ğ“¤ Î±) :=
  diagonal_nonempty.principal_neBot.mono refl_le_uniformity

theorem refl_mem_uniformity {x : Î±} {s : Set (Î± Ã— Î±)} (h : s âˆˆ ğ“¤ Î±) : (x, x) âˆˆ s :=
  refl_le_uniformity h rfl

theorem mem_uniformity_of_eq {x y : Î±} {s : Set (Î± Ã— Î±)} (h : s âˆˆ ğ“¤ Î±) (hx : x = y) : (x, y) âˆˆ s :=
  refl_le_uniformity h hx

theorem symm_le_uniformity : map (@Prod.swap Î± Î±) (ğ“¤ _) â‰¤ ğ“¤ _ :=
  UniformSpace.symm

theorem comp_le_uniformity : ((ğ“¤ Î±).lift' fun s : Set (Î± Ã— Î±) => s â—‹ s) â‰¤ ğ“¤ Î± :=
  UniformSpace.comp

theorem lift'_comp_uniformity : ((ğ“¤ Î±).lift' fun s : Set (Î± Ã— Î±) => s â—‹ s) = ğ“¤ Î± :=
  comp_le_uniformity.antisymm <| le_lift'.2 fun _s hs â†¦ mem_of_superset hs <|
    subset_comp_self <| idRel_subset.2 fun _ â†¦ refl_mem_uniformity hs

theorem tendsto_swap_uniformity : Tendsto (@Prod.swap Î± Î±) (ğ“¤ Î±) (ğ“¤ Î±) :=
  symm_le_uniformity

theorem comp_mem_uniformity_sets {s : Set (Î± Ã— Î±)} (hs : s âˆˆ ğ“¤ Î±) : âˆƒ t âˆˆ ğ“¤ Î±, t â—‹ t âŠ† s :=
  (mem_lift'_sets <| monotone_id.compRel monotone_id).mp <| comp_le_uniformity hs

/-- Relation `fun f g â†¦ Tendsto (fun x â†¦ (f x, g x)) l (ğ“¤ Î±)` is transitive. -/
theorem Filter.Tendsto.uniformity_trans {l : Filter Î²} {fâ‚ fâ‚‚ fâ‚ƒ : Î² â†’ Î±}
    (hâ‚â‚‚ : Tendsto (fun x => (fâ‚ x, fâ‚‚ x)) l (ğ“¤ Î±))
    (hâ‚‚â‚ƒ : Tendsto (fun x => (fâ‚‚ x, fâ‚ƒ x)) l (ğ“¤ Î±)) : Tendsto (fun x => (fâ‚ x, fâ‚ƒ x)) l (ğ“¤ Î±) := by
  refine le_trans (le_lift'.2 fun s hs => mem_map.2 ?_) comp_le_uniformity
  filter_upwards [mem_map.1 (hâ‚â‚‚ hs), mem_map.1 (hâ‚‚â‚ƒ hs)] with x hxâ‚â‚‚ hxâ‚‚â‚ƒ using âŸ¨_, hxâ‚â‚‚, hxâ‚‚â‚ƒâŸ©

/-- Relation `fun f g â†¦ Tendsto (fun x â†¦ (f x, g x)) l (ğ“¤ Î±)` is symmetric. -/
theorem Filter.Tendsto.uniformity_symm {l : Filter Î²} {f : Î² â†’ Î± Ã— Î±} (h : Tendsto f l (ğ“¤ Î±)) :
    Tendsto (fun x => ((f x).2, (f x).1)) l (ğ“¤ Î±) :=
  tendsto_swap_uniformity.comp h

/-- Relation `fun f g â†¦ Tendsto (fun x â†¦ (f x, g x)) l (ğ“¤ Î±)` is reflexive. -/
theorem tendsto_diag_uniformity (f : Î² â†’ Î±) (l : Filter Î²) :
    Tendsto (fun x => (f x, f x)) l (ğ“¤ Î±) := fun _s hs =>
  mem_map.2 <| univ_mem' fun _ => refl_mem_uniformity hs

theorem tendsto_const_uniformity {a : Î±} {f : Filter Î²} : Tendsto (fun _ => (a, a)) f (ğ“¤ Î±) :=
  tendsto_diag_uniformity (fun _ => a) f

theorem symm_of_uniformity {s : Set (Î± Ã— Î±)} (hs : s âˆˆ ğ“¤ Î±) :
    âˆƒ t âˆˆ ğ“¤ Î±, (âˆ€ a b, (a, b) âˆˆ t â†’ (b, a) âˆˆ t) âˆ§ t âŠ† s :=
  have : preimage Prod.swap s âˆˆ ğ“¤ Î± := symm_le_uniformity hs
  âŸ¨s âˆ© preimage Prod.swap s, inter_mem hs this, fun _ _ âŸ¨hâ‚, hâ‚‚âŸ© => âŸ¨hâ‚‚, hâ‚âŸ©, inter_subset_leftâŸ©

theorem comp_symm_of_uniformity {s : Set (Î± Ã— Î±)} (hs : s âˆˆ ğ“¤ Î±) :
    âˆƒ t âˆˆ ğ“¤ Î±, (âˆ€ {a b}, (a, b) âˆˆ t â†’ (b, a) âˆˆ t) âˆ§ t â—‹ t âŠ† s :=
  let âŸ¨_t, htâ‚, htâ‚‚âŸ© := comp_mem_uniformity_sets hs
  let âŸ¨t', ht', ht'â‚, ht'â‚‚âŸ© := symm_of_uniformity htâ‚
  âŸ¨t', ht', ht'â‚ _ _, Subset.trans (monotone_id.compRel monotone_id ht'â‚‚) htâ‚‚âŸ©

theorem uniformity_le_symm : ğ“¤ Î± â‰¤ map Prod.swap (ğ“¤ Î±) := by
  rw [map_swap_eq_comap_swap]; exact tendsto_swap_uniformity.le_comap

theorem uniformity_eq_symm : ğ“¤ Î± = map Prod.swap (ğ“¤ Î±) :=
  le_antisymm uniformity_le_symm symm_le_uniformity

@[simp]
theorem comap_swap_uniformity : comap (@Prod.swap Î± Î±) (ğ“¤ Î±) = ğ“¤ Î± :=
  (congr_arg _ uniformity_eq_symm).trans <| comap_map Prod.swap_injective

theorem symmetrize_mem_uniformity {V : Set (Î± Ã— Î±)} (h : V âˆˆ ğ“¤ Î±) : symmetrizeRel V âˆˆ ğ“¤ Î± := by
  apply (ğ“¤ Î±).inter_sets h
  rw [â† image_swap_eq_preimage_swap, uniformity_eq_symm]
  exact image_mem_map h

/-- Symmetric entourages form a basis of `ğ“¤ Î±` -/
theorem UniformSpace.hasBasis_symmetric :
    (ğ“¤ Î±).HasBasis (fun s : Set (Î± Ã— Î±) => s âˆˆ ğ“¤ Î± âˆ§ IsSymmetricRel s) id :=
  hasBasis_self.2 fun t t_in =>
    âŸ¨symmetrizeRel t, symmetrize_mem_uniformity t_in, symmetric_symmetrizeRel t,
      symmetrizeRel_subset_self tâŸ©

theorem uniformity_lift_le_swap {g : Set (Î± Ã— Î±) â†’ Filter Î²} {f : Filter Î²} (hg : Monotone g)
    (h : ((ğ“¤ Î±).lift fun s => g (preimage Prod.swap s)) â‰¤ f) : (ğ“¤ Î±).lift g â‰¤ f :=
  calc
    (ğ“¤ Î±).lift g â‰¤ (Filter.map (@Prod.swap Î± Î±) <| ğ“¤ Î±).lift g :=
      lift_mono uniformity_le_symm le_rfl
    _ â‰¤ _ := by rw [map_lift_eq2 hg, image_swap_eq_preimage_swap]; exact h

theorem uniformity_lift_le_comp {f : Set (Î± Ã— Î±) â†’ Filter Î²} (h : Monotone f) :
    ((ğ“¤ Î±).lift fun s => f (s â—‹ s)) â‰¤ (ğ“¤ Î±).lift f :=
  calc
    ((ğ“¤ Î±).lift fun s => f (s â—‹ s)) = ((ğ“¤ Î±).lift' fun s : Set (Î± Ã— Î±) => s â—‹ s).lift f := by
      rw [lift_lift'_assoc]
      Â· exact monotone_id.compRel monotone_id
      Â· exact h
    _ â‰¤ (ğ“¤ Î±).lift f := lift_mono comp_le_uniformity le_rfl

theorem comp3_mem_uniformity {s : Set (Î± Ã— Î±)} (hs : s âˆˆ ğ“¤ Î±) : âˆƒ t âˆˆ ğ“¤ Î±, t â—‹ (t â—‹ t) âŠ† s :=
  let âŸ¨_t', ht', ht'sâŸ© := comp_mem_uniformity_sets hs
  let âŸ¨t, ht, htt'âŸ© := comp_mem_uniformity_sets ht'
  âŸ¨t, ht, (compRel_mono ((subset_comp_self (refl_le_uniformity ht)).trans htt') htt').trans ht'sâŸ©

/-- See also `comp3_mem_uniformity`. -/
theorem comp_le_uniformity3 : ((ğ“¤ Î±).lift' fun s : Set (Î± Ã— Î±) => s â—‹ (s â—‹ s)) â‰¤ ğ“¤ Î± := fun _ h =>
  let âŸ¨_t, htU, htâŸ© := comp3_mem_uniformity h
  mem_of_superset (mem_lift' htU) ht

/-- See also `comp_open_symm_mem_uniformity_sets`. -/
theorem comp_symm_mem_uniformity_sets {s : Set (Î± Ã— Î±)} (hs : s âˆˆ ğ“¤ Î±) :
    âˆƒ t âˆˆ ğ“¤ Î±, IsSymmetricRel t âˆ§ t â—‹ t âŠ† s := by
  obtain âŸ¨w, w_in, w_subâŸ© : âˆƒ w âˆˆ ğ“¤ Î±, w â—‹ w âŠ† s := comp_mem_uniformity_sets hs
  use symmetrizeRel w, symmetrize_mem_uniformity w_in, symmetric_symmetrizeRel w
  have : symmetrizeRel w âŠ† w := symmetrizeRel_subset_self w
  calc symmetrizeRel w â—‹ symmetrizeRel w
    _ âŠ† w â—‹ w := by gcongr
    _ âŠ† s     := w_sub

theorem subset_comp_self_of_mem_uniformity {s : Set (Î± Ã— Î±)} (h : s âˆˆ ğ“¤ Î±) : s âŠ† s â—‹ s :=
  subset_comp_self (refl_le_uniformity h)

theorem comp_comp_symm_mem_uniformity_sets {s : Set (Î± Ã— Î±)} (hs : s âˆˆ ğ“¤ Î±) :
    âˆƒ t âˆˆ ğ“¤ Î±, IsSymmetricRel t âˆ§ t â—‹ t â—‹ t âŠ† s := by
  rcases comp_symm_mem_uniformity_sets hs with âŸ¨w, w_in, _, w_subâŸ©
  rcases comp_symm_mem_uniformity_sets w_in with âŸ¨t, t_in, t_symm, t_subâŸ©
  use t, t_in, t_symm
  have : t âŠ† t â—‹ t := subset_comp_self_of_mem_uniformity t_in
  calc
    t â—‹ t â—‹ t âŠ† w â—‹ (t â—‹ t) := by gcongr
    _ âŠ† w â—‹ w := by gcongr
    _ âŠ† s := w_sub

/-!
### Balls in uniform spaces
-/

namespace UniformSpace

/-- The ball around `(x : Î²)` with respect to `(V : Set (Î² Ã— Î²))`. Intended to be
used for `V âˆˆ ğ“¤ Î²`, but this is not needed for the definition. Recovers the
notions of metric space ball when `V = {p | dist p.1 p.2 < r }`. -/
def ball (x : Î²) (V : Set (Î² Ã— Î²)) : Set Î² := Prod.mk x â»Â¹' V

open UniformSpace (ball)

lemma mem_ball_self (x : Î±) {V : Set (Î± Ã— Î±)} : V âˆˆ ğ“¤ Î± â†’ x âˆˆ ball x V := refl_mem_uniformity

/-- The triangle inequality for `UniformSpace.ball` -/
theorem mem_ball_comp {V W : Set (Î² Ã— Î²)} {x y z} (h : y âˆˆ ball x V) (h' : z âˆˆ ball y W) :
    z âˆˆ ball x (V â—‹ W) :=
  prodMk_mem_compRel h h'

theorem ball_subset_of_comp_subset {V W : Set (Î² Ã— Î²)} {x y} (h : x âˆˆ ball y W) (h' : W â—‹ W âŠ† V) :
    ball x W âŠ† ball y V := fun _z z_in => h' (mem_ball_comp h z_in)

theorem ball_mono {V W : Set (Î² Ã— Î²)} (h : V âŠ† W) (x : Î²) : ball x V âŠ† ball x W :=
  preimage_mono h

theorem ball_inter (x : Î²) (V W : Set (Î² Ã— Î²)) : ball x (V âˆ© W) = ball x V âˆ© ball x W :=
  preimage_inter

theorem ball_inter_left (x : Î²) (V W : Set (Î² Ã— Î²)) : ball x (V âˆ© W) âŠ† ball x V :=
  ball_mono inter_subset_left x

theorem ball_inter_right (x : Î²) (V W : Set (Î² Ã— Î²)) : ball x (V âˆ© W) âŠ† ball x W :=
  ball_mono inter_subset_right x

theorem ball_iInter {x : Î²} {V : Î¹ â†’ Set (Î² Ã— Î²)} : ball x (â‹‚ i, V i) = â‹‚ i, ball x (V i) :=
  preimage_iInter

theorem mem_ball_symmetry {V : Set (Î² Ã— Î²)} (hV : IsSymmetricRel V) {x y} :
    x âˆˆ ball y V â†” y âˆˆ ball x V :=
  show (x, y) âˆˆ Prod.swap â»Â¹' V â†” (x, y) âˆˆ V by
    unfold IsSymmetricRel at hV
    rw [hV]

theorem ball_eq_of_symmetry {V : Set (Î² Ã— Î²)} (hV : IsSymmetricRel V) {x} :
    ball x V = { y | (y, x) âˆˆ V } := by
  ext y
  rw [mem_ball_symmetry hV]
  exact Iff.rfl

theorem mem_comp_of_mem_ball {V W : Set (Î² Ã— Î²)} {x y z : Î²} (hV : IsSymmetricRel V)
    (hx : x âˆˆ ball z V) (hy : y âˆˆ ball z W) : (x, y) âˆˆ V â—‹ W := by
  rw [mem_ball_symmetry hV] at hx
  exact âŸ¨z, hx, hyâŸ©

theorem mem_comp_comp {V W M : Set (Î² Ã— Î²)} (hW' : IsSymmetricRel W) {p : Î² Ã— Î²} :
    p âˆˆ V â—‹ M â—‹ W â†” (ball p.1 V Ã—Ë¢ ball p.2 W âˆ© M).Nonempty := by
  obtain âŸ¨x, yâŸ© := p
  constructor
  Â· rintro âŸ¨z, âŸ¨w, hpw, hwzâŸ©, hzyâŸ©
    exact âŸ¨(w, z), âŸ¨hpw, by rwa [mem_ball_symmetry hW']âŸ©, hwzâŸ©
  Â· rintro âŸ¨âŸ¨w, zâŸ©, âŸ¨w_in, z_inâŸ©, hwzâŸ©
    rw [mem_ball_symmetry hW'] at z_in
    exact âŸ¨z, âŸ¨w, w_in, hwzâŸ©, z_inâŸ©

end UniformSpace

/-!
### Neighborhoods in uniform spaces
-/

open UniformSpace

theorem mem_nhds_uniformity_iff_right {x : Î±} {s : Set Î±} :
    s âˆˆ ğ“ x â†” { p : Î± Ã— Î± | p.1 = x â†’ p.2 âˆˆ s } âˆˆ ğ“¤ Î± := by
  simp only [nhds_eq_comap_uniformity, mem_comap_prodMk]

theorem mem_nhds_uniformity_iff_left {x : Î±} {s : Set Î±} :
    s âˆˆ ğ“ x â†” { p : Î± Ã— Î± | p.2 = x â†’ p.1 âˆˆ s } âˆˆ ğ“¤ Î± := by
  rw [uniformity_eq_symm, mem_nhds_uniformity_iff_right]
  simp only [mem_map, preimage_setOf_eq, Prod.snd_swap, Prod.fst_swap]

theorem nhdsWithin_eq_comap_uniformity_of_mem {x : Î±} {T : Set Î±} (hx : x âˆˆ T) (S : Set Î±) :
    ğ“[S] x = (ğ“¤ Î± âŠ“ ğ“Ÿ (T Ã—Ë¢ S)).comap (Prod.mk x) := by
  simp [nhdsWithin, nhds_eq_comap_uniformity, hx]

theorem nhdsWithin_eq_comap_uniformity {x : Î±} (S : Set Î±) :
    ğ“[S] x = (ğ“¤ Î± âŠ“ ğ“Ÿ (univ Ã—Ë¢ S)).comap (Prod.mk x) :=
  nhdsWithin_eq_comap_uniformity_of_mem (mem_univ _) S

/-- See also `isOpen_iff_isOpen_ball_subset`. -/
theorem isOpen_iff_ball_subset {s : Set Î±} : IsOpen s â†” âˆ€ x âˆˆ s, âˆƒ V âˆˆ ğ“¤ Î±, ball x V âŠ† s := by
  simp_rw [isOpen_iff_mem_nhds, nhds_eq_comap_uniformity, mem_comap, ball]

theorem nhds_basis_uniformity' {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set (Î± Ã— Î±)} (h : (ğ“¤ Î±).HasBasis p s)
    {x : Î±} : (ğ“ x).HasBasis p fun i => ball x (s i) := by
  rw [nhds_eq_comap_uniformity]
  exact h.comap (Prod.mk x)

theorem nhds_basis_uniformity {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set (Î± Ã— Î±)} (h : (ğ“¤ Î±).HasBasis p s)
    {x : Î±} : (ğ“ x).HasBasis p fun i => { y | (y, x) âˆˆ s i } := by
  replace h := h.comap Prod.swap
  rw [comap_swap_uniformity] at h
  exact nhds_basis_uniformity' h

theorem nhds_eq_comap_uniformity' {x : Î±} : ğ“ x = (ğ“¤ Î±).comap fun y => (y, x) :=
  (nhds_basis_uniformity (ğ“¤ Î±).basis_sets).eq_of_same_basis <| (ğ“¤ Î±).basis_sets.comap _

theorem UniformSpace.mem_nhds_iff {x : Î±} {s : Set Î±} : s âˆˆ ğ“ x â†” âˆƒ V âˆˆ ğ“¤ Î±, ball x V âŠ† s := by
  rw [nhds_eq_comap_uniformity, mem_comap]
  simp_rw [ball]

theorem UniformSpace.ball_mem_nhds (x : Î±) â¦ƒV : Set (Î± Ã— Î±)â¦„ (V_in : V âˆˆ ğ“¤ Î±) : ball x V âˆˆ ğ“ x := by
  rw [UniformSpace.mem_nhds_iff]
  exact âŸ¨V, V_in, Subset.rflâŸ©

theorem UniformSpace.ball_mem_nhdsWithin {x : Î±} {S : Set Î±} â¦ƒV : Set (Î± Ã— Î±)â¦„ (x_in : x âˆˆ S)
    (V_in : V âˆˆ ğ“¤ Î± âŠ“ ğ“Ÿ (S Ã—Ë¢ S)) : ball x V âˆˆ ğ“[S] x := by
  rw [nhdsWithin_eq_comap_uniformity_of_mem x_in, mem_comap]
  exact âŸ¨V, V_in, Subset.rflâŸ©

theorem UniformSpace.mem_nhds_iff_symm {x : Î±} {s : Set Î±} :
    s âˆˆ ğ“ x â†” âˆƒ V âˆˆ ğ“¤ Î±, IsSymmetricRel V âˆ§ ball x V âŠ† s := by
  rw [UniformSpace.mem_nhds_iff]
  constructor
  Â· rintro âŸ¨V, V_in, V_subâŸ©
    use symmetrizeRel V, symmetrize_mem_uniformity V_in, symmetric_symmetrizeRel V
    exact Subset.trans (ball_mono (symmetrizeRel_subset_self V) x) V_sub
  Â· rintro âŸ¨V, V_in, _, V_subâŸ©
    exact âŸ¨V, V_in, V_subâŸ©

theorem UniformSpace.hasBasis_nhds (x : Î±) :
    HasBasis (ğ“ x) (fun s : Set (Î± Ã— Î±) => s âˆˆ ğ“¤ Î± âˆ§ IsSymmetricRel s) fun s => ball x s :=
  âŸ¨fun t => by simp [UniformSpace.mem_nhds_iff_symm, and_assoc]âŸ©

open UniformSpace

theorem UniformSpace.mem_closure_iff_symm_ball {s : Set Î±} {x} :
    x âˆˆ closure s â†” âˆ€ {V}, V âˆˆ ğ“¤ Î± â†’ IsSymmetricRel V â†’ (s âˆ© ball x V).Nonempty := by
  simp [mem_closure_iff_nhds_basis (hasBasis_nhds x), Set.Nonempty]

theorem UniformSpace.mem_closure_iff_ball {s : Set Î±} {x} :
    x âˆˆ closure s â†” âˆ€ {V}, V âˆˆ ğ“¤ Î± â†’ (ball x V âˆ© s).Nonempty := by
  simp [mem_closure_iff_nhds_basis' (nhds_basis_uniformity' (ğ“¤ Î±).basis_sets)]

theorem nhds_eq_uniformity {x : Î±} : ğ“ x = (ğ“¤ Î±).lift' (ball x) :=
  (nhds_basis_uniformity' (ğ“¤ Î±).basis_sets).eq_biInf

theorem nhds_eq_uniformity' {x : Î±} : ğ“ x = (ğ“¤ Î±).lift' fun s => { y | (y, x) âˆˆ s } :=
  (nhds_basis_uniformity (ğ“¤ Î±).basis_sets).eq_biInf

theorem mem_nhds_left (x : Î±) {s : Set (Î± Ã— Î±)} (h : s âˆˆ ğ“¤ Î±) : { y : Î± | (x, y) âˆˆ s } âˆˆ ğ“ x :=
  ball_mem_nhds x h

theorem mem_nhds_right (y : Î±) {s : Set (Î± Ã— Î±)} (h : s âˆˆ ğ“¤ Î±) : { x : Î± | (x, y) âˆˆ s } âˆˆ ğ“ y :=
  mem_nhds_left _ (symm_le_uniformity h)

theorem exists_mem_nhds_ball_subset_of_mem_nhds {a : Î±} {U : Set Î±} (h : U âˆˆ ğ“ a) :
    âˆƒ V âˆˆ ğ“ a, âˆƒ t âˆˆ ğ“¤ Î±, âˆ€ a' âˆˆ V, UniformSpace.ball a' t âŠ† U :=
  let âŸ¨t, ht, htUâŸ© := comp_mem_uniformity_sets (mem_nhds_uniformity_iff_right.1 h)
  âŸ¨_, mem_nhds_left a ht, t, ht, fun aâ‚ hâ‚ aâ‚‚ hâ‚‚ => @htU (a, aâ‚‚) âŸ¨aâ‚, hâ‚, hâ‚‚âŸ© rflâŸ©

theorem tendsto_right_nhds_uniformity {a : Î±} : Tendsto (fun a' => (a', a)) (ğ“ a) (ğ“¤ Î±) := fun _ =>
  mem_nhds_right a

theorem tendsto_left_nhds_uniformity {a : Î±} : Tendsto (fun a' => (a, a')) (ğ“ a) (ğ“¤ Î±) := fun _ =>
  mem_nhds_left a

theorem lift_nhds_left {x : Î±} {g : Set Î± â†’ Filter Î²} (hg : Monotone g) :
    (ğ“ x).lift g = (ğ“¤ Î±).lift fun s : Set (Î± Ã— Î±) => g (ball x s) := by
  rw [nhds_eq_comap_uniformity, comap_lift_eq2 hg]
  simp_rw [ball, Function.comp_def]

theorem lift_nhds_right {x : Î±} {g : Set Î± â†’ Filter Î²} (hg : Monotone g) :
    (ğ“ x).lift g = (ğ“¤ Î±).lift fun s : Set (Î± Ã— Î±) => g { y | (y, x) âˆˆ s } := by
  rw [nhds_eq_comap_uniformity', comap_lift_eq2 hg]
  simp_rw [Function.comp_def, preimage]

theorem nhds_nhds_eq_uniformity_uniformity_prod {a b : Î±} :
    ğ“ a Ã—Ë¢ ğ“ b = (ğ“¤ Î±).lift fun s : Set (Î± Ã— Î±) =>
      (ğ“¤ Î±).lift' fun t => { y : Î± | (y, a) âˆˆ s } Ã—Ë¢ { y : Î± | (b, y) âˆˆ t } := by
  rw [nhds_eq_uniformity', nhds_eq_uniformity, prod_lift'_lift']
  exacts [rfl, monotone_preimage, monotone_preimage]

theorem Filter.HasBasis.biInter_biUnion_ball {p : Î¹ â†’ Prop} {U : Î¹ â†’ Set (Î± Ã— Î±)}
    (h : HasBasis (ğ“¤ Î±) p U) (s : Set Î±) :
    (â‹‚ (i) (_ : p i), â‹ƒ x âˆˆ s, ball x (U i)) = closure s := by
  ext x
  simp [mem_closure_iff_nhds_basis (nhds_basis_uniformity h), ball]

/-! ### Uniform continuity -/


/-- A function `f : Î± â†’ Î²` is *uniformly continuous* if `(f x, f y)` tends to the diagonal
as `(x, y)` tends to the diagonal. In other words, if `x` is sufficiently close to `y`, then
`f x` is close to `f y` no matter where `x` and `y` are located in `Î±`. -/
def UniformContinuous [UniformSpace Î²] (f : Î± â†’ Î²) :=
  Tendsto (fun x : Î± Ã— Î± => (f x.1, f x.2)) (ğ“¤ Î±) (ğ“¤ Î²)

/-- Notation for uniform continuity with respect to non-standard `UniformSpace` instances. -/
scoped[Uniformity] notation "UniformContinuous[" uâ‚ ", " uâ‚‚ "]" => @UniformContinuous _ _ uâ‚ uâ‚‚

/-- A function `f : Î± â†’ Î²` is *uniformly continuous* on `s : Set Î±` if `(f x, f y)` tends to
the diagonal as `(x, y)` tends to the diagonal while remaining in `s Ã—Ë¢ s`.
In other words, if `x` is sufficiently close to `y`, then `f x` is close to
`f y` no matter where `x` and `y` are located in `s`. -/
def UniformContinuousOn [UniformSpace Î²] (f : Î± â†’ Î²) (s : Set Î±) : Prop :=
  Tendsto (fun x : Î± Ã— Î± => (f x.1, f x.2)) (ğ“¤ Î± âŠ“ ğ“Ÿ (s Ã—Ë¢ s)) (ğ“¤ Î²)

theorem uniformContinuous_def [UniformSpace Î²] {f : Î± â†’ Î²} :
    UniformContinuous f â†” âˆ€ r âˆˆ ğ“¤ Î², { x : Î± Ã— Î± | (f x.1, f x.2) âˆˆ r } âˆˆ ğ“¤ Î± :=
  Iff.rfl

theorem uniformContinuous_iff_eventually [UniformSpace Î²] {f : Î± â†’ Î²} :
    UniformContinuous f â†” âˆ€ r âˆˆ ğ“¤ Î², âˆ€á¶  x : Î± Ã— Î± in ğ“¤ Î±, (f x.1, f x.2) âˆˆ r :=
  Iff.rfl

theorem uniformContinuousOn_univ [UniformSpace Î²] {f : Î± â†’ Î²} :
    UniformContinuousOn f univ â†” UniformContinuous f := by
  rw [UniformContinuousOn, UniformContinuous, univ_prod_univ, principal_univ, inf_top_eq]

theorem uniformContinuous_of_const [UniformSpace Î²] {c : Î± â†’ Î²} (h : âˆ€ a b, c a = c b) :
    UniformContinuous c :=
  have : (fun x : Î± Ã— Î± => (c x.fst, c x.snd)) â»Â¹' idRel = univ :=
    eq_univ_iff_forall.2 fun âŸ¨a, bâŸ© => h a b
  le_trans (map_le_iff_le_comap.2 <| by simp [comap_principal, this]) refl_le_uniformity

theorem uniformContinuous_id : UniformContinuous (@id Î±) := tendsto_id

theorem uniformContinuous_const [UniformSpace Î²] {b : Î²} : UniformContinuous fun _ : Î± => b :=
  uniformContinuous_of_const fun _ _ => rfl

nonrec theorem UniformContinuous.comp [UniformSpace Î²] [UniformSpace Î³] {g : Î² â†’ Î³} {f : Î± â†’ Î²}
    (hg : UniformContinuous g) (hf : UniformContinuous f) : UniformContinuous (g âˆ˜ f) :=
  hg.comp hf

/-- If a function `T` is uniformly continuous in a uniform space `Î²`,
then its `n`-th iterate `T^[n]` is also uniformly continuous. -/
theorem UniformContinuous.iterate [UniformSpace Î²] (T : Î² â†’ Î²) (n : â„•) (h : UniformContinuous T) :
    UniformContinuous T^[n] := by
  induction n with
  | zero => exact uniformContinuous_id
  | succ n hn => exact Function.iterate_succ _ _ â–¸ UniformContinuous.comp hn h

theorem Filter.HasBasis.uniformContinuous_iff {Î¹'} [UniformSpace Î²] {p : Î¹ â†’ Prop}
    {s : Î¹ â†’ Set (Î± Ã— Î±)} (ha : (ğ“¤ Î±).HasBasis p s) {q : Î¹' â†’ Prop} {t : Î¹' â†’ Set (Î² Ã— Î²)}
    (hb : (ğ“¤ Î²).HasBasis q t) {f : Î± â†’ Î²} :
    UniformContinuous f â†” âˆ€ i, q i â†’ âˆƒ j, p j âˆ§ âˆ€ x y, (x, y) âˆˆ s j â†’ (f x, f y) âˆˆ t i :=
  (ha.tendsto_iff hb).trans <| by simp only [Prod.forall]

theorem Filter.HasBasis.uniformContinuousOn_iff {Î¹'} [UniformSpace Î²] {p : Î¹ â†’ Prop}
    {s : Î¹ â†’ Set (Î± Ã— Î±)} (ha : (ğ“¤ Î±).HasBasis p s) {q : Î¹' â†’ Prop} {t : Î¹' â†’ Set (Î² Ã— Î²)}
    (hb : (ğ“¤ Î²).HasBasis q t) {f : Î± â†’ Î²} {S : Set Î±} :
    UniformContinuousOn f S â†”
      âˆ€ i, q i â†’ âˆƒ j, p j âˆ§ âˆ€ x, x âˆˆ S â†’ âˆ€ y, y âˆˆ S â†’ (x, y) âˆˆ s j â†’ (f x, f y) âˆˆ t i :=
  ((ha.inf_principal (S Ã—Ë¢ S)).tendsto_iff hb).trans <| by
    simp_rw [Prod.forall, Set.inter_comm (s _), forall_mem_comm, mem_inter_iff, mem_prod, and_imp]

end UniformSpace
