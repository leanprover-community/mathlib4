/-
Copyright (c) 2019 Reid Barton. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl
-/
import Mathlib.Topology.Constructions
import Mathlib.Order.Filter.ListTraverse
import Mathlib.Tactic.AdaptationNote
import Mathlib.Topology.Algebra.Monoid.Defs

/-!
# Topology on lists and vectors

-/


open TopologicalSpace Set Filter

open Topology

variable {Î± : Type*} {Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²]

instance : TopologicalSpace (List Î±) :=
  TopologicalSpace.mkOfNhds (traverse nhds)

theorem nhds_list (as : List Î±) : ğ“ as = traverse ğ“ as := by
  refine nhds_mkOfNhds _ _ ?_ ?_
  Â· intro l
    induction l with
    | nil => exact le_rfl
    | cons a l ih =>
      suffices List.cons <$> pure a <*> pure l â‰¤ List.cons <$> ğ“ a <*> traverse ğ“ l by
        simpa only [functor_norm] using this
      exact Filter.seq_mono (Filter.map_mono <| pure_le_nhds a) ih
  Â· intro l s hs
    rcases (mem_traverse_iff _ _).1 hs with âŸ¨u, hu, husâŸ©
    clear as hs
    have : âˆƒ v : List (Set Î±), l.Forallâ‚‚ (fun a s => IsOpen s âˆ§ a âˆˆ s) v âˆ§ sequence v âŠ† s := by
      induction hu generalizing s with
      | nil =>
        exists []
        simp only [List.forallâ‚‚_nil_left_iff]
        exact âŸ¨trivial, husâŸ©
      | cons ht _ ih =>
        rcases mem_nhds_iff.1 ht with âŸ¨u, hut, huâŸ©
        rcases ih _ Subset.rfl with âŸ¨v, hv, hvssâŸ©
        exact
          âŸ¨u::v, List.Forallâ‚‚.cons hu hv,
            Subset.trans (Set.seq_mono (Set.image_mono hut) hvss) husâŸ©
    rcases this with âŸ¨v, hv, hvsâŸ©
    have : sequence v âˆˆ traverse ğ“ l :=
      mem_traverse _ _ <| hv.imp fun a s âŸ¨hs, haâŸ© => IsOpen.mem_nhds hs ha
    refine mem_of_superset this fun u hu â†¦ ?_
    have hu := (List.mem_traverse _ _).1 hu
    have : List.Forallâ‚‚ (fun a s => IsOpen s âˆ§ a âˆˆ s) u v := by
      refine List.Forallâ‚‚.flip ?_
      replace hv := hv.flip
      simp only [List.forallâ‚‚_and_left, Function.flip_def] at hv âŠ¢
      exact âŸ¨hv.1, hu.flipâŸ©
    refine mem_of_superset ?_ hvs
    exact mem_traverse _ _ (this.imp fun a s âŸ¨hs, haâŸ© => IsOpen.mem_nhds hs ha)

@[simp]
theorem nhds_nil : ğ“ ([] : List Î±) = pure [] := by
  rw [nhds_list, List.traverse_nil _]

theorem nhds_cons (a : Î±) (l : List Î±) : ğ“ (a::l) = List.cons <$> ğ“ a <*> ğ“ l := by
  rw [nhds_list, List.traverse_cons _, â† nhds_list]

theorem List.tendsto_cons {a : Î±} {l : List Î±} :
    Tendsto (fun p : Î± Ã— List Î± => List.cons p.1 p.2) (ğ“ a Ã—Ë¢ ğ“ l) (ğ“ (a::l)) := by
  rw [nhds_cons, Tendsto, Filter.map_prod]; exact le_rfl

theorem Filter.Tendsto.cons {Î± : Type*} {f : Î± â†’ Î²} {g : Î± â†’ List Î²} {a : Filter Î±} {b : Î²}
    {l : List Î²} (hf : Tendsto f a (ğ“ b)) (hg : Tendsto g a (ğ“ l)) :
    Tendsto (fun a => List.cons (f a) (g a)) a (ğ“ (b::l)) :=
  List.tendsto_cons.comp (Tendsto.prodMk hf hg)

namespace List

theorem tendsto_cons_iff {Î² : Type*} {f : List Î± â†’ Î²} {b : Filter Î²} {a : Î±} {l : List Î±} :
    Tendsto f (ğ“ (a::l)) b â†” Tendsto (fun p : Î± Ã— List Î± => f (p.1::p.2)) (ğ“ a Ã—Ë¢ ğ“ l) b := by
  have : ğ“ (a::l) = (ğ“ a Ã—Ë¢ ğ“ l).map fun p : Î± Ã— List Î± => p.1::p.2 := by
    simp only [nhds_cons, Filter.prod_eq, (Filter.map_def _ _).symm,
      (Filter.seq_eq_filter_seq _ _).symm]
    simp [-Filter.map_def, Function.comp_def, functor_norm]
  rw [this, Filter.tendsto_map'_iff]; rfl

theorem continuous_cons : Continuous fun x : Î± Ã— List Î± => (x.1::x.2 : List Î±) :=
  continuous_iff_continuousAt.mpr fun âŸ¨_x, _yâŸ© => continuousAt_fst.cons continuousAt_snd

theorem tendsto_nhds {Î² : Type*} {f : List Î± â†’ Î²} {r : List Î± â†’ Filter Î²}
    (h_nil : Tendsto f (pure []) (r []))
    (h_cons :
      âˆ€ l a,
        Tendsto f (ğ“ l) (r l) â†’
          Tendsto (fun p : Î± Ã— List Î± => f (p.1::p.2)) (ğ“ a Ã—Ë¢ ğ“ l) (r (a::l))) :
    âˆ€ l, Tendsto f (ğ“ l) (r l)
  | [] => by rwa [nhds_nil]
  | a::l => by
    rw [tendsto_cons_iff]; exact h_cons l a (@tendsto_nhds _ _ _ h_nil h_cons l)

instance [DiscreteTopology Î±] : DiscreteTopology (List Î±) := by
  rw [discreteTopology_iff_nhds]; intro l; induction l <;> simp [*, nhds_cons]

theorem continuousAt_length : âˆ€ l : List Î±, ContinuousAt List.length l := by
  simp only [ContinuousAt, nhds_discrete]
  refine tendsto_nhds ?_ ?_
  Â· exact tendsto_pure_pure _ _
  Â· intro l a ih
    dsimp only [List.length]
    refine Tendsto.comp (tendsto_pure_pure (fun x => x + 1) _) ?_
    exact Tendsto.comp ih tendsto_snd

/-- Continuity of `insertIdx` in terms of `Tendsto`. -/
theorem tendsto_insertIdx' {a : Î±} :
    âˆ€ {n : â„•} {l : List Î±},
      Tendsto (fun p : Î± Ã— List Î± => p.2.insertIdx n p.1) (ğ“ a Ã—Ë¢ ğ“ l) (ğ“ (l.insertIdx n a))
  | 0, _ => tendsto_cons
  | n + 1, [] => by simp
  | n + 1, a'::l => by
    have : ğ“ a Ã—Ë¢ ğ“ (a'::l) =
        (ğ“ a Ã—Ë¢ (ğ“ a' Ã—Ë¢ ğ“ l)).map fun p : Î± Ã— Î± Ã— List Î± => (p.1, p.2.1::p.2.2) := by
      simp only [nhds_cons, Filter.prod_eq, â† Filter.map_def, â† Filter.seq_eq_filter_seq]
      simp [-Filter.map_def, Function.comp_def, functor_norm]
    rw [this, tendsto_map'_iff]
    exact
      (tendsto_fst.comp tendsto_snd).cons
        ((@tendsto_insertIdx' _ n l).comp <| tendsto_fst.prodMk <| tendsto_snd.comp tendsto_snd)

theorem tendsto_insertIdx {Î²} {n : â„•} {a : Î±} {l : List Î±} {f : Î² â†’ Î±} {g : Î² â†’ List Î±}
    {b : Filter Î²} (hf : Tendsto f b (ğ“ a)) (hg : Tendsto g b (ğ“ l)) :
    Tendsto (fun b : Î² => (g b).insertIdx n (f b)) b (ğ“ (l.insertIdx n a)) :=
  tendsto_insertIdx'.comp (hf.prodMk hg)

theorem continuous_insertIdx {n : â„•} : Continuous fun p : Î± Ã— List Î± => p.2.insertIdx n p.1 :=
  continuous_iff_continuousAt.mpr fun âŸ¨a, lâŸ© => by
    rw [ContinuousAt, nhds_prod_eq]; exact tendsto_insertIdx'

theorem tendsto_eraseIdx :
    âˆ€ {n : â„•} {l : List Î±}, Tendsto (eraseIdx Â· n) (ğ“ l) (ğ“ (eraseIdx l n))
  | _, [] => by rw [nhds_nil]; exact tendsto_pure_nhds _ _
  | 0, a::l => by rw [tendsto_cons_iff]; exact tendsto_snd
  | n + 1, a::l => by
    rw [tendsto_cons_iff]
    dsimp [eraseIdx]
    exact tendsto_fst.cons ((@tendsto_eraseIdx n l).comp tendsto_snd)

theorem continuous_eraseIdx {n : â„•} : Continuous fun l : List Î± => eraseIdx l n :=
  continuous_iff_continuousAt.mpr fun _a => tendsto_eraseIdx

@[to_additive]
theorem tendsto_prod [MulOneClass Î±] [ContinuousMul Î±] {l : List Î±} :
    Tendsto List.prod (ğ“ l) (ğ“ l.prod) := by
  induction l with
  | nil => simp +contextual [nhds_nil, mem_of_mem_nhds, tendsto_pure_left]
  | cons x l ih =>
    simp_rw [tendsto_cons_iff, prod_cons]
    have := continuous_iff_continuousAt.mp continuous_mul (x, l.prod)
    rw [ContinuousAt, nhds_prod_eq] at this
    exact this.comp (tendsto_id.prodMap ih)

@[to_additive]
theorem continuous_prod [MulOneClass Î±] [ContinuousMul Î±] : Continuous (prod : List Î± â†’ Î±) :=
  continuous_iff_continuousAt.mpr fun _l => tendsto_prod

end List

namespace List.Vector

instance (n : â„•) : TopologicalSpace (Vector Î± n) := by unfold Vector; infer_instance

theorem tendsto_cons {n : â„•} {a : Î±} {l : Vector Î± n} :
    Tendsto (fun p : Î± Ã— Vector Î± n => p.1 ::áµ¥ p.2) (ğ“ a Ã—Ë¢ ğ“ l) (ğ“ (a ::áµ¥ l)) := by
  rw [tendsto_subtype_rng, Vector.cons_val]
  exact tendsto_fst.cons (Tendsto.comp continuousAt_subtype_val tendsto_snd)

theorem tendsto_insertIdx {n : â„•} {i : Fin (n + 1)} {a : Î±} :
    âˆ€ {l : Vector Î± n},
      Tendsto (fun p : Î± Ã— Vector Î± n => insertIdx p.1 i p.2) (ğ“ a Ã—Ë¢ ğ“ l)
        (ğ“ (insertIdx a i l))
  | âŸ¨l, hlâŸ© => by
    rw [insertIdx, tendsto_subtype_rng]
    simp only [insertIdx_val]
    exact List.tendsto_insertIdx tendsto_fst (Tendsto.comp continuousAt_subtype_val tendsto_snd : _)

/-- Continuity of `Vector.insertIdx`. -/
theorem continuous_insertIdx' {n : â„•} {i : Fin (n + 1)} :
    Continuous fun p : Î± Ã— Vector Î± n => Vector.insertIdx p.1 i p.2 :=
  continuous_iff_continuousAt.mpr fun âŸ¨a, lâŸ© => by
    rw [ContinuousAt, nhds_prod_eq]; exact tendsto_insertIdx

theorem continuous_insertIdx {n : â„•} {i : Fin (n + 1)} {f : Î² â†’ Î±} {g : Î² â†’ Vector Î± n}
    (hf : Continuous f) (hg : Continuous g) : Continuous fun b => Vector.insertIdx (f b) i (g b) :=
  continuous_insertIdx'.comp (hf.prodMk hg)

theorem continuousAt_eraseIdx {n : â„•} {i : Fin (n + 1)} :
    âˆ€ {l : Vector Î± (n + 1)}, ContinuousAt (Vector.eraseIdx i) l
  | âŸ¨l, hlâŸ© => by
    rw [ContinuousAt, Vector.eraseIdx, tendsto_subtype_rng]
    simp only [Vector.eraseIdx_val]
    exact Tendsto.comp List.tendsto_eraseIdx continuousAt_subtype_val

theorem continuous_eraseIdx {n : â„•} {i : Fin (n + 1)} :
    Continuous (Vector.eraseIdx i : Vector Î± (n + 1) â†’ Vector Î± n) :=
  continuous_iff_continuousAt.mpr fun âŸ¨_a, _lâŸ© => continuousAt_eraseIdx

end List.Vector
