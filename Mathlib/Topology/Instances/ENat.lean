import Mathlib.Data.ENat.Lattice
import Mathlib.Topology.Algebra.Order.Compact
import Mathlib.Algebra.CharP.Defs

open ENat Filter Topology TopologicalSpace Set

instance : TopologicalSpace â„•âˆ := Preorder.topology â„•âˆ

instance : OrderTopology â„•âˆ := OrderTopology.mk rfl

theorem isOpen_singleton (n : â„•) : IsOpen {(n : â„•âˆ)} := by
  cases n with
  | zero =>
    refine GenerateOpen.basic _ âŸ¨1, Or.inr ?_âŸ©
    ext x
    simp only [CharP.cast_eq_zero, Set.mem_singleton_iff, Set.mem_setOf_eq]
    exact lt_one_iff_eq_zero.symm
  | succ k =>
    have : {(â†‘(k + 1) : â„•âˆ)} = (Set.Iio â†‘(k + 2)) âˆ© (Set.Ioi â†‘k) := by
      rw [Iio_inter_Ioi]
      ext x
      simp only [mem_singleton_iff, mem_Ioo]
      rcases eq_or_ne x âŠ¤ with h | h
      Â· simp only [h, not_top_lt, and_false, iff_false]
        exact top_ne_coe _
      Â· lift x to â„• using h
        norm_cast
        omega
    rw [this]
    apply GenerateOpen.inter <;> constructor
    Â· exact âŸ¨(â†‘(k + 2) : â„•âˆ), Or.inr rflâŸ©
    Â· exact âŸ¨k, Or.inl rflâŸ©

theorem isOpen_top_not_mem (s : Set â„•âˆ) (h : âŠ¤ âˆ‰ s) : IsOpen s := by
  rw [â† Set.biUnion_of_singleton s]
  refine isOpen_biUnion fun x hx â†¦ ?_
  lift x to â„• using ne_of_mem_of_not_mem hx h
  exact isOpen_singleton x

theorem ENat.coe_max (a b : â„•) : (â†‘(max a b) : â„•âˆ) = max â†‘a â†‘b := by
  apply eq_max <;> try norm_cast
  Â· exact le_max_left _ _
  Â· exact le_max_right _ _
  Â· intro d h1 h2
    rcases max_choice a b with h | h <;> rwa [h]

theorem isOpen_iff_top_mem (s : Set â„•âˆ) (top_mem : âŠ¤ âˆˆ s) :
    IsOpen s â†” âˆƒ x : â„•, Set.Ioi â†‘x âŠ† s where
  mp hs := by
    rcases (nhds_top_basis.mem_iff' s).1 (hs.mem_nhds top_mem) with âŸ¨x, x_lt, hxâŸ©
    lift x to â„• using x_lt.ne
    exact âŸ¨x, hxâŸ©
  mpr := by
    rintro âŸ¨a, haâŸ©
    rw [â† Set.inter_union_compl s (Set.Ioi a)]
    apply IsOpen.union
    Â· rw [Set.inter_eq_self_of_subset_right ha]
      exact GenerateOpen.basic _ âŸ¨a, Or.inl rflâŸ©
    Â· apply isOpen_top_not_mem
      simp [top_mem]

theorem ENat.tendsto_coe_atTop :
    Tendsto (@Nat.cast â„•âˆ _) atTop (ğ“ âŠ¤) := by
  rw [tendsto_atTop_nhds]
  intro U mem_U hU
  rcases (isOpen_iff_top_mem _ mem_U).1 hU with âŸ¨x, hUâŸ©
  refine âŸ¨x + 1, fun n hn â†¦ hU ?_âŸ©
  simp only [Set.mem_Ioi, Nat.cast_lt]
  omega

variable {X : Type*} (f : â„• â†’ X) (x : X)

def compactSequence : â„•âˆ â†’ X := fun n â†¦
  match n with
  | some k => f k
  | none => x

theorem compactSequence_coe (n : â„•) : compactSequence f x n = f n := rfl

theorem compactSequence_ne_top (n : â„•âˆ) (hn : n â‰  âŠ¤) : compactSequence f x n = f n.toNat := by
  lift n to â„• using hn
  rfl

theorem compactSequence_top : compactSequence f x âŠ¤ = x := rfl

theorem range_compactSequence : range (compactSequence f x) = insert x (range f) := by
  ext y
  constructor
  Â· rintro âŸ¨n, rflâŸ©
    rcases eq_or_ne n âŠ¤ with rfl | hn
    Â· exact mem_insert _ _
    Â· exact mem_insert_of_mem _ âŸ¨n.toNat, (compactSequence_ne_top f x n hn).symmâŸ©
  Â· rw [mem_insert_iff]
    rintro (rfl | âŸ¨n, rflâŸ©)
    Â· exact âŸ¨âŠ¤, rflâŸ©
    Â· exact âŸ¨n, rflâŸ©

variable [TopologicalSpace X]

theorem continuous_compactSequence (h : Tendsto f atTop (ğ“ x)) :
    Continuous (compactSequence f x) where
  isOpen_preimage s hs := by
    by_cases htop : âŠ¤ âˆˆ (compactSequence f x â»Â¹' s)
    Â· rw [isOpen_iff_top_mem _ htop]
      rcases tendsto_atTop_nhds.1 h s htop hs with âŸ¨N, hNâŸ©
      refine âŸ¨N, fun y hy â†¦ ?_âŸ©
      rcases eq_or_ne y âŠ¤ with rfl | y_ne_top
      Â· exact htop
      Â· lift y to â„• using y_ne_top
        exact hN _ (by simpa using hy : N < y).le
    Â· exact isOpen_top_not_mem _ htop

theorem isCompact_range_compactSequence (h : Tendsto f atTop (ğ“ x)) :
    IsCompact (range (compactSequence f x)) := isCompact_range (continuous_compactSequence f x h)
