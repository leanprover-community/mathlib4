/-
Copyright (c) 2022 Peter Nelson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Peter Nelson, Kalle KytÃ¶lÃ¤
-/
import Mathlib.Data.ENat.Basic
import Mathlib.Topology.Algebra.Monoid
import Mathlib.Topology.Instances.Discrete
import Mathlib.Order.Interval.Set.WithBotTop
import Mathlib.Topology.Order.T5

/-!
# Topology on the extended natural numbers
-/

noncomputable section

open Set Filter Function Topology
open scoped ENat

variable {Î± : Type*} {Î² : Type*} {Î³ : Type*}

namespace ENat

variable {a b c d : â„•âˆ} {r p q : â„•} {x y z : â„•âˆ} {s : Set â„•âˆ}

open TopologicalSpace

/-- Topology on `â„•âˆ`.

Note: this is different from the `EMetricSpace` topology. The `EMetricSpace` topology has
`IsOpen {âˆ}`, while all neighborhoods of `âˆ` in `â„•âˆ` contain infinite intervals. -/
instance : TopologicalSpace â„•âˆ := Preorder.topology â„•âˆ

instance : OrderTopology â„•âˆ := âŸ¨rflâŸ©

example : OrderClosedTopology â„•âˆ := by infer_instance

-- short-circuit type class inference
instance : T2Space â„•âˆ := inferInstance
instance : T5Space â„•âˆ := inferInstance
instance : T4Space â„•âˆ := inferInstance

theorem isEmbedding_natCast : IsEmbedding ((â†‘) : â„• â†’ â„•âˆ) :=
  Nat.strictMono_cast.isEmbedding_of_ordConnected <| range_natCast â–¸ ordConnected_Iio

@[deprecated (since := "2024-10-26")]
alias embedding_natCast := isEmbedding_natCast

theorem isOpenEmbedding_natCast : IsOpenEmbedding ((â†‘) : â„• â†’ â„•âˆ) :=
  âŸ¨isEmbedding_natCast, range_natCast â–¸ isOpen_IioâŸ©

@[deprecated (since := "2024-10-18")]
alias openEmbedding_natCast := isOpenEmbedding_natCast

theorem nhds_natCast (n : â„•) : ğ“ (n : â„•âˆ) = pure (n : â„•âˆ) := by
  simp [â† isOpenEmbedding_natCast.map_nhds_eq]

@[simp]
protected theorem nhds_eq_pure {n : â„•âˆ} (h : n â‰  âŠ¤) : ğ“ n = pure n := by
  lift n to â„• using h
  simp [nhds_natCast]

theorem isOpen_singleton {x : â„•âˆ} (hx : x â‰  âŠ¤) : IsOpen {x} := by
  rw [isOpen_singleton_iff_nhds_eq_pure, ENat.nhds_eq_pure hx]

theorem mem_nhds_iff {x : â„•âˆ} {s : Set â„•âˆ} (hx : x â‰  âŠ¤) : s âˆˆ ğ“ x â†” x âˆˆ s := by
  simp [hx]

theorem mem_nhds_natCast_iff (n : â„•) {s : Set â„•âˆ} : s âˆˆ ğ“ (n : â„•âˆ) â†” (n : â„•âˆ) âˆˆ s :=
  mem_nhds_iff (coe_ne_top _)

theorem tendsto_nhds_top_iff_natCast_lt {Î± : Type*} {l : Filter Î±} {f : Î± â†’ â„•âˆ} :
    Tendsto f l (ğ“ âŠ¤) â†” âˆ€ n : â„•, âˆ€á¶  a in l, n < f a := by
  simp_rw [nhds_top_order, lt_top_iff_ne_top, tendsto_iInf, tendsto_principal]
  exact Option.ball_ne_none

instance : ContinuousAdd â„•âˆ := by
  refine âŸ¨continuous_iff_continuousAt.2 fun (a, b) â†¦ ?_âŸ©
  match a, b with
  | âŠ¤, _ => exact tendsto_nhds_top_mono' continuousAt_fst fun p â†¦ le_add_right le_rfl
  | (a : â„•), âŠ¤ => exact tendsto_nhds_top_mono' continuousAt_snd fun p â†¦ le_add_left le_rfl
  | (a : â„•), (b : â„•) => simp [ContinuousAt, nhds_prod_eq, tendsto_pure_nhds]

instance : ContinuousMul â„•âˆ where
  continuous_mul :=
    have key (a : â„•âˆ) : ContinuousAt (Â· * Â·).uncurry (a, âŠ¤) := by
      rcases (zero_le a).eq_or_gt with rfl | ha
      Â· simp [ContinuousAt, nhds_prod_eq]
      Â· simp only [ContinuousAt, Function.uncurry, mul_top ha.ne']
        refine tendsto_nhds_top_mono continuousAt_snd ?_
        filter_upwards [continuousAt_fst (lt_mem_nhds ha)] with (x, y) (hx : 0 < x)
        exact le_mul_of_one_le_left (zero_le y) (Order.one_le_iff_pos.2 hx)
    continuous_iff_continuousAt.2 <| Prod.forall.2 fun
      | (a : â„•âˆ), âŠ¤ => key a
      | âŠ¤, (b : â„•âˆ) =>
        ((key b).comp_of_eq (continuous_swap.tendsto (âŠ¤, b)) rfl).congr <|
          .of_forall fun _ â†¦ mul_comm ..
      | (a : â„•), (b : â„•) => by
        simp [ContinuousAt, nhds_prod_eq, tendsto_pure_nhds]

protected theorem continuousAt_sub {a b : â„•âˆ} (h : a â‰  âŠ¤ âˆ¨ b â‰  âŠ¤) :
    ContinuousAt (Â· - Â·).uncurry (a, b) := by
  match a, b, h with
  | (a : â„•), (b : â„•), _ => simp [ContinuousAt, nhds_prod_eq]
  | (a : â„•), âŠ¤, _ =>
    suffices âˆ€á¶  b in ğ“ âŠ¤, (a - b : â„•âˆ) = 0 by
      simpa [ContinuousAt, nhds_prod_eq, tsub_eq_zero_of_le]
    filter_upwards [le_mem_nhds (WithTop.coe_lt_top a)] with b using tsub_eq_zero_of_le
  | âŠ¤, (b : â„•), _ =>
    suffices âˆ€ n : â„•, âˆ€á¶  a : â„•âˆ in ğ“ âŠ¤, b + n < a by
      simpa [ContinuousAt, nhds_prod_eq, (Â· âˆ˜ Â·), lt_tsub_iff_left, tendsto_nhds_top_iff_natCast_lt]
    exact fun n â†¦ lt_mem_nhds <| WithTop.coe_lt_top (b + n)

theorem isOpen_ne_top : IsOpen {a : â„•âˆ | a â‰  âŠ¤} := isOpen_ne

theorem isOpen_Ico_zero : IsOpen (Ico 0 b) := by rw [ENat.Ico_eq_Iio]; exact isOpen_Iio

theorem continuous_coe_iff {Î±} [TopologicalSpace Î±] {f : Î± â†’ â„•} :
    (Continuous fun a â†¦ (f a : â„•âˆ)) â†” Continuous f :=
  isEmbedding_natCast.continuous_iff.symm

theorem nhds_coe {r : â„•} : ğ“ (r : â„•âˆ) = (ğ“ r).map (â†‘) :=
  (isOpenEmbedding_natCast.map_nhds_eq r).symm

theorem nhds_top : ğ“ (âŠ¤ : â„•âˆ) = â¨… (a) (_ : a â‰  âŠ¤), ğ“Ÿ (Ioi a) :=
  nhds_top_order.trans <| by simp [lt_top_iff_ne_top, Ioi]

theorem nhds_top_hasBasis : (ğ“ (âŠ¤ : â„•âˆ)).HasBasis (fun a â†¦ a < âŠ¤) fun a â†¦ Ioi a :=
  _root_.nhds_top_basis

lemma isOpen_Ico : IsOpen (Ico a b) := by
  by_cases a_zero : a = 0
  Â· simpa [a_zero, ENat.Ico_eq_Iio] using isOpen_Iio
  Â· simpa [ENat.Ico_eq_Ioo a_zero b] using isOpen_Ioo

lemma isOpen_Ioc : IsOpen (Ioc a b) := by
  by_cases b_top : b = âŠ¤
  Â· simpa [b_top, ENat.Ioc_eq_Ioi] using isOpen_Ioi
  Â· simpa [ENat.Ioc_eq_Ioo a b_top] using isOpen_Ioo

lemma isOpen_Icc (h : a â‰  âŠ¤ âˆ¨ b â‰  âŠ¤) : IsOpen (Icc a b) := by
  by_cases b_top : b = âŠ¤
  Â· simp only [ne_eq, b_top, not_true_eq_false, or_false, Icc_top] at h âŠ¢
    by_cases a_zero : a = 0
    Â· convert isOpen_univ
      ext x
      simp [a_zero]
    Â· simpa [ENat.Ici_eq_Ioi a_zero h] using isOpen_Ioi
  Â· simpa [ENat.Icc_eq_Ico _ b_top] using isOpen_Ico

@[simp] lemma mem_nhds_iff_of_ne_top {n : â„•âˆ} (n_ne_top : n â‰  âŠ¤) (s : Set â„•âˆ) :
    s âˆˆ ğ“ n â†” n âˆˆ s := by
  refine âŸ¨fun h â†¦ mem_of_mem_nhds h, fun h â†¦ ?_âŸ©
  apply mem_of_superset ((ENat.isOpen_singleton n_ne_top).mem_nhds rfl)
  exact singleton_subset_iff.mpr h

theorem tendsto_nhds_coe_iff {Î± : Type*} {l : Filter Î±} {x : â„•} {f : â„•âˆ â†’ Î±} :
    Tendsto f (ğ“ â†‘x) l â†” Tendsto (f âˆ˜ (â†‘) : â„• â†’ Î±) (ğ“ x) l := by
  rw [nhds_coe, tendsto_map'_iff]

theorem continuousAt_coe_iff {Î± : Type*} [TopologicalSpace Î±] {x : â„•} {f : â„•âˆ â†’ Î±} :
    ContinuousAt f â†‘x â†” ContinuousAt (f âˆ˜ (â†‘) : â„• â†’ Î±) x :=
  tendsto_nhds_coe_iff

theorem nhds_coe_coe {r p : â„•} :
    ğ“ ((r : â„•âˆ), (p : â„•âˆ)) = (ğ“ (r, p)).map fun p : â„• Ã— â„• => (â†‘p.1, â†‘p.2) :=
  ((isOpenEmbedding_natCast.prodMap isOpenEmbedding_natCast).map_nhds_eq (r, p)).symm

theorem tendsto_toNat {a : â„•âˆ} (ha : a â‰  âŠ¤) :
    Tendsto ENat.toNat (ğ“ a) (ğ“ a.toNat) := by
  lift a to â„• using ha
  rw [nhds_coe, tendsto_map'_iff]
  exact tendsto_id

theorem eventuallyEq_of_toNat_eventuallyEq {l : Filter Î±} {f g : Î± â†’ â„•âˆ}
    (hfi : âˆ€á¶  x in l, f x â‰  âŠ¤) (hgi : âˆ€á¶  x in l, g x â‰  âŠ¤)
    (hfg : (fun x => (f x).toNat) =á¶ [l] fun x => (g x).toNat) : f =á¶ [l] g := by
  filter_upwards [hfi, hgi, hfg] with _ hfx hgx _
  rwa [â† ENat.toNat_eq_toNat hfx hgx]

theorem continuousOn_toNat : ContinuousOn ENat.toNat {a | a â‰  âŠ¤} := fun _a ha =>
  ContinuousAt.continuousWithinAt (tendsto_toNat ha)

lemma continuousAt_toNat (hx : x â‰  âŠ¤) : ContinuousAt ENat.toNat x :=
  continuousOn_toNat.continuousAt (isOpen_ne_top.mem_nhds_iff.mpr hx)

theorem tendsto_nhds_top_iff_nat {m : Î± â†’ â„•âˆ} {f : Filter Î±} :
    Tendsto m f (ğ“ âŠ¤) â†” âˆ€ n : â„•, âˆ€á¶  i in f, n < m i := by
  simp only [nhds_top, ne_eq, tendsto_iInf, tendsto_principal, mem_Ioi]
  exact âŸ¨fun h k â†¦ h k (coe_ne_top k), fun h n n_ne_top â†¦ (coe_toNat n_ne_top).symm â–¸ h n.toNatâŸ©

end ENat

theorem Filter.Tendsto.enatSub {Î± : Type*} {l : Filter Î±} {f g : Î± â†’ â„•âˆ} {a b : â„•âˆ}
    (hf : Tendsto f l (ğ“ a)) (hg : Tendsto g l (ğ“ b)) (h : a â‰  âŠ¤ âˆ¨ b â‰  âŠ¤) :
    Tendsto (fun x â†¦ f x - g x) l (ğ“ (a - b)) :=
  (ENat.continuousAt_sub h).tendsto.comp (hf.prodMk_nhds hg)

variable {X : Type*} [TopologicalSpace X] {f g : X â†’ â„•âˆ} {s : Set X} {x : X}

nonrec theorem ContinuousWithinAt.enatSub
    (hf : ContinuousWithinAt f s x) (hg : ContinuousWithinAt g s x) (h : f x â‰  âŠ¤ âˆ¨ g x â‰  âŠ¤) :
    ContinuousWithinAt (fun x â†¦ f x - g x) s x :=
  hf.enatSub hg h

nonrec theorem ContinuousAt.enatSub
    (hf : ContinuousAt f x) (hg : ContinuousAt g x) (h : f x â‰  âŠ¤ âˆ¨ g x â‰  âŠ¤) :
    ContinuousAt (fun x â†¦ f x - g x) x :=
  hf.enatSub hg h

nonrec theorem ContinuousOn.enatSub
    (hf : ContinuousOn f s) (hg : ContinuousOn g s) (h : âˆ€ x âˆˆ s, f x â‰  âŠ¤ âˆ¨ g x â‰  âŠ¤) :
    ContinuousOn (fun x â†¦ f x - g x) s := fun x hx â†¦
  (hf x hx).enatSub (hg x hx) (h x hx)

nonrec theorem Continuous.enatSub
    (hf : Continuous f) (hg : Continuous g) (h : âˆ€ x, f x â‰  âŠ¤ âˆ¨ g x â‰  âŠ¤) :
    Continuous (fun x â†¦ f x - g x) :=
  continuous_iff_continuousAt.2 fun x â†¦ hf.continuousAt.enatSub hg.continuousAt (h x)
