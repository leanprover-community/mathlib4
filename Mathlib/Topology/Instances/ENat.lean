/-
Copyright (c) 2022 Peter Nelson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Peter Nelson
-/
import Mathlib.Data.ENat.Basic
import Mathlib.Topology.Algebra.Monoid
import Mathlib.Topology.Instances.Discrete

/-!
# Topology on extended natural numbers
-/

open Set Filter
open scoped Topology

namespace ENat

instance : TopologicalSpace â„•âˆž := Preorder.topology â„•âˆž

instance : OrderTopology â„•âˆž := âŸ¨rflâŸ©

@[simp] theorem range_natCast : range ((â†‘) : â„• â†’ â„•âˆž) = Iio âŠ¤ :=
  WithTop.range_coe

theorem embedding_natCast : Embedding ((â†‘) : â„• â†’ â„•âˆž) :=
  Nat.strictMono_cast.embedding_of_ordConnected <| range_natCast â–¸ ordConnected_Iio

theorem openEmbedding_natCast : OpenEmbedding ((â†‘) : â„• â†’ â„•âˆž) :=
  âŸ¨embedding_natCast, range_natCast â–¸ isOpen_IioâŸ©

theorem nhds_natCast (n : â„•) : ð“ (n : â„•âˆž) = pure (n : â„•âˆž) := by
  simp [â† openEmbedding_natCast.map_nhds_eq]

@[simp]
protected theorem nhds_eq_pure {n : â„•âˆž} (h : n â‰  âŠ¤) : ð“ n = pure n := by
  lift n to â„• using h
  simp [nhds_natCast]

theorem isOpen_singleton {x : â„•âˆž} (hx : x â‰  âŠ¤) : IsOpen {x} := by
  rw [isOpen_singleton_iff_nhds_eq_pure, ENat.nhds_eq_pure hx]

theorem mem_nhds_iff {x : â„•âˆž} {s : Set â„•âˆž} (hx : x â‰  âŠ¤) : s âˆˆ ð“ x â†” x âˆˆ s := by
  simp [hx]

theorem mem_nhds_natCast_iff (n : â„•) {s : Set â„•âˆž} : s âˆˆ ð“ (n : â„•âˆž) â†” (n : â„•âˆž) âˆˆ s :=
  mem_nhds_iff (coe_ne_top _)

instance : ContinuousAdd â„•âˆž := by
  refine âŸ¨continuous_iff_continuousAt.2 fun (a, b) â†¦ ?_âŸ©
  match a, b with
  | âŠ¤, _ => exact tendsto_nhds_top_mono' continuousAt_fst fun p â†¦ le_add_right le_rfl
  | (a : â„•), âŠ¤ => exact tendsto_nhds_top_mono' continuousAt_snd fun p â†¦ le_add_left le_rfl
  | (a : â„•), (b : â„•) => simp [ContinuousAt, nhds_prod_eq, tendsto_pure_nhds]

instance : ContinuousMul â„•âˆž where
  continuous_mul :=
    have key (a : â„•âˆž) : ContinuousAt (Â· * Â·).uncurry (a, âŠ¤) := by
      rcases (zero_le a).eq_or_gt with rfl | ha
      Â· simp [ContinuousAt, nhds_prod_eq]
      Â· simp only [ContinuousAt, Function.uncurry, mul_top ha.ne']
        refine tendsto_nhds_top_mono continuousAt_snd ?_
        filter_upwards [continuousAt_fst (lt_mem_nhds ha)] with (x, y) (hx : 0 < x)
        exact le_mul_of_one_le_left (zero_le y) (ENat.one_le_iff_pos.2 hx)
    continuous_iff_continuousAt.2 <| Prod.forall.2 fun
      | (a : â„•âˆž), âŠ¤ => key a
      | âŠ¤, (b : â„•âˆž) =>
        ((key b).comp_of_eq (continuous_swap.tendsto (âŠ¤, b)) rfl).congr <|
          eventually_of_forall fun _ â†¦ mul_comm ..
      | (a : â„•), (b : â„•) => by
        simp [ContinuousAt, nhds_prod_eq, tendsto_pure_nhds]

end ENat
