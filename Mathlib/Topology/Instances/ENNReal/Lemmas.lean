/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl
-/
import Mathlib.Algebra.BigOperators.Intervals
import Mathlib.Data.ENNReal.BigOperators
import Mathlib.Tactic.Bound
import Mathlib.Topology.Order.LiminfLimsup
import Mathlib.Topology.EMetricSpace.Lipschitz
import Mathlib.Topology.Instances.NNReal.Lemmas
import Mathlib.Topology.MetricSpace.Pseudo.Real
import Mathlib.Topology.MetricSpace.ProperSpace.Real
import Mathlib.Topology.Metrizable.Uniformity

/-!
# Topology on extended non-negative reals
-/

noncomputable section

open Filter Function Metric Set Topology
open scoped Finset ENNReal NNReal

variable {Î± : Type*} {Î² : Type*} {Î³ : Type*}

namespace ENNReal

variable {a b : â„â‰¥0âˆ} {r : â„â‰¥0} {x : â„â‰¥0âˆ} {Îµ : â„â‰¥0âˆ}

section TopologicalSpace

open TopologicalSpace

theorem isOpen_ne_top : IsOpen { a : â„â‰¥0âˆ | a â‰  âˆ } := isOpen_ne

theorem isOpen_Ico_zero : IsOpen (Ico 0 b) := by
  rw [ENNReal.Ico_eq_Iio]
  exact isOpen_Iio

theorem coe_range_mem_nhds : range ((â†‘) : â„â‰¥0 â†’ â„â‰¥0âˆ) âˆˆ ğ“ (r : â„â‰¥0âˆ) :=
  IsOpen.mem_nhds isOpenEmbedding_coe.isOpen_range <| mem_range_self _

@[fun_prop]
theorem continuous_coe : Continuous ((â†‘) : â„â‰¥0 â†’ â„â‰¥0âˆ) :=
  isEmbedding_coe.continuous

lemma tendsto_coe_toNNReal {a : â„â‰¥0âˆ} (ha : a â‰  âŠ¤) : Tendsto (â†‘) (ğ“ a.toNNReal) (ğ“ a) := by
  nth_rewrite 2 [â† coe_toNNReal ha]
  exact continuous_coe.tendsto _

theorem continuous_coe_iff {Î±} [TopologicalSpace Î±] {f : Î± â†’ â„â‰¥0} :
    (Continuous fun a => (f a : â„â‰¥0âˆ)) â†” Continuous f :=
  isEmbedding_coe.continuous_iff.symm

theorem nhds_coe {r : â„â‰¥0} : ğ“ (r : â„â‰¥0âˆ) = (ğ“ r).map (â†‘) :=
  (isOpenEmbedding_coe.map_nhds_eq r).symm

theorem tendsto_nhds_coe_iff {Î± : Type*} {l : Filter Î±} {x : â„â‰¥0} {f : â„â‰¥0âˆ â†’ Î±} :
    Tendsto f (ğ“ â†‘x) l â†” Tendsto (f âˆ˜ (â†‘) : â„â‰¥0 â†’ Î±) (ğ“ x) l := by
  rw [nhds_coe, tendsto_map'_iff]

theorem continuousAt_coe_iff {Î± : Type*} [TopologicalSpace Î±] {x : â„â‰¥0} {f : â„â‰¥0âˆ â†’ Î±} :
    ContinuousAt f â†‘x â†” ContinuousAt (f âˆ˜ (â†‘) : â„â‰¥0 â†’ Î±) x :=
  tendsto_nhds_coe_iff

theorem continuous_ofReal : Continuous ENNReal.ofReal :=
  (continuous_coe_iff.2 continuous_id).comp continuous_real_toNNReal

theorem tendsto_ofReal {f : Filter Î±} {m : Î± â†’ â„} {a : â„} (h : Tendsto m f (ğ“ a)) :
    Tendsto (fun a => ENNReal.ofReal (m a)) f (ğ“ (ENNReal.ofReal a)) :=
  (continuous_ofReal.tendsto a).comp h

theorem tendsto_toNNReal {a : â„â‰¥0âˆ} (ha : a â‰  âˆ) :
    Tendsto ENNReal.toNNReal (ğ“ a) (ğ“ a.toNNReal) := by
  lift a to â„â‰¥0 using ha
  rw [nhds_coe, tendsto_map'_iff]
  exact tendsto_id

theorem tendsto_toNNReal_iff {f : Î± â†’ â„â‰¥0âˆ} {u : Filter Î±} (ha : a â‰  âˆ) (hf : âˆ€ x, f x â‰  âˆ) :
    Tendsto (ENNReal.toNNReal âˆ˜ f) u (ğ“ (a.toNNReal)) â†” Tendsto f u (ğ“ a) := by
  refine âŸ¨fun h => ?_, fun h => (ENNReal.tendsto_toNNReal ha).comp hâŸ©
  rw [â† coe_comp_toNNReal_comp hf]
  exact (tendsto_coe_toNNReal ha).comp h

theorem tendsto_toNNReal_iff' {f : Î± â†’ â„â‰¥0âˆ} {u : Filter Î±} {a : â„â‰¥0} (hf : âˆ€ x, f x â‰  âˆ) :
    Tendsto (ENNReal.toNNReal âˆ˜ f) u (ğ“ a) â†” Tendsto f u (ğ“ a) := by
  rw [â† toNNReal_coe a]
  exact tendsto_toNNReal_iff coe_ne_top hf

theorem eventuallyEq_of_toReal_eventuallyEq {l : Filter Î±} {f g : Î± â†’ â„â‰¥0âˆ}
    (hfi : âˆ€á¶  x in l, f x â‰  âˆ) (hgi : âˆ€á¶  x in l, g x â‰  âˆ)
    (hfg : (fun x => (f x).toReal) =á¶ [l] fun x => (g x).toReal) : f =á¶ [l] g := by
  filter_upwards [hfi, hgi, hfg] with _ hfx hgx _
  rwa [â† ENNReal.toReal_eq_toReal hfx hgx]

theorem continuousOn_toNNReal : ContinuousOn ENNReal.toNNReal { a | a â‰  âˆ } := fun _a ha =>
  ContinuousAt.continuousWithinAt (tendsto_toNNReal ha)

theorem tendsto_toReal {a : â„â‰¥0âˆ} (ha : a â‰  âˆ) : Tendsto ENNReal.toReal (ğ“ a) (ğ“ a.toReal) :=
  NNReal.tendsto_coe.2 <| tendsto_toNNReal ha

lemma continuousOn_toReal : ContinuousOn ENNReal.toReal { a | a â‰  âˆ } :=
  NNReal.continuous_coe.comp_continuousOn continuousOn_toNNReal

lemma continuousAt_toReal (hx : x â‰  âˆ) : ContinuousAt ENNReal.toReal x :=
  continuousOn_toReal.continuousAt (isOpen_ne_top.mem_nhds_iff.mpr hx)

/-- The set of finite `â„â‰¥0âˆ` numbers is homeomorphic to `â„â‰¥0`. -/
def neTopHomeomorphNNReal : { a | a â‰  âˆ } â‰ƒâ‚œ â„â‰¥0 where
  toEquiv := neTopEquivNNReal
  continuous_toFun := continuousOn_iff_continuous_restrict.1 continuousOn_toNNReal
  continuous_invFun := continuous_coe.subtype_mk _

/-- The set of finite `â„â‰¥0âˆ` numbers is homeomorphic to `â„â‰¥0`. -/
def ltTopHomeomorphNNReal : { a | a < âˆ } â‰ƒâ‚œ â„â‰¥0 := by
  refine (Homeomorph.setCongr ?_).trans neTopHomeomorphNNReal
  simp only [lt_top_iff_ne_top]

theorem nhds_top : ğ“ âˆ = â¨… (a) (_ : a â‰  âˆ), ğ“Ÿ (Ioi a) :=
  nhds_top_order.trans <| by simp [lt_top_iff_ne_top, Ioi]

theorem nhds_top' : ğ“ âˆ = â¨… r : â„â‰¥0, ğ“Ÿ (Ioi â†‘r) :=
  nhds_top.trans <| iInf_ne_top _

theorem nhds_top_basis : (ğ“ âˆ).HasBasis (fun a => a < âˆ) fun a => Ioi a :=
  _root_.nhds_top_basis

theorem tendsto_nhds_top_iff_nnreal {m : Î± â†’ â„â‰¥0âˆ} {f : Filter Î±} :
    Tendsto m f (ğ“ âˆ) â†” âˆ€ x : â„â‰¥0, âˆ€á¶  a in f, â†‘x < m a := by
  simp only [nhds_top', tendsto_iInf, tendsto_principal, mem_Ioi]

theorem tendsto_nhds_top_iff_nat {m : Î± â†’ â„â‰¥0âˆ} {f : Filter Î±} :
    Tendsto m f (ğ“ âˆ) â†” âˆ€ n : â„•, âˆ€á¶  a in f, â†‘n < m a :=
  tendsto_nhds_top_iff_nnreal.trans
    âŸ¨fun h n => by simpa only [ENNReal.coe_natCast] using h n, fun h x =>
      let âŸ¨n, hnâŸ© := exists_nat_gt x
      (h n).mono fun _ => lt_trans <| by rwa [â† ENNReal.coe_natCast, coe_lt_coe]âŸ©

theorem tendsto_nhds_top {m : Î± â†’ â„â‰¥0âˆ} {f : Filter Î±} (h : âˆ€ n : â„•, âˆ€á¶  a in f, â†‘n < m a) :
    Tendsto m f (ğ“ âˆ) :=
  tendsto_nhds_top_iff_nat.2 h

theorem tendsto_nat_nhds_top : Tendsto (fun n : â„• => â†‘n) atTop (ğ“ âˆ) :=
  tendsto_nhds_top fun n =>
    mem_atTop_sets.2 âŸ¨n + 1, fun _m hm => mem_setOf.2 <| Nat.cast_lt.2 <| Nat.lt_of_succ_le hmâŸ©

@[simp, norm_cast]
theorem tendsto_coe_nhds_top {f : Î± â†’ â„â‰¥0} {l : Filter Î±} :
    Tendsto (fun x => (f x : â„â‰¥0âˆ)) l (ğ“ âˆ) â†” Tendsto f l atTop := by
  rw [tendsto_nhds_top_iff_nnreal, atTop_basis_Ioi.tendsto_right_iff]; simp

@[simp]
theorem tendsto_ofReal_nhds_top {f : Î± â†’ â„} {l : Filter Î±} :
    Tendsto (fun x â†¦ ENNReal.ofReal (f x)) l (ğ“ âˆ) â†” Tendsto f l atTop :=
  tendsto_coe_nhds_top.trans Real.tendsto_toNNReal_atTop_iff

theorem tendsto_ofReal_atTop : Tendsto ENNReal.ofReal atTop (ğ“ âˆ) :=
  tendsto_ofReal_nhds_top.2 tendsto_id

theorem nhds_zero : ğ“ (0 : â„â‰¥0âˆ) = â¨… (a) (_ : a â‰  0), ğ“Ÿ (Iio a) :=
  nhds_bot_order.trans <| by simp [pos_iff_ne_zero, Iio]

theorem nhds_zero_basis : (ğ“ (0 : â„â‰¥0âˆ)).HasBasis (fun a : â„â‰¥0âˆ => 0 < a) fun a => Iio a :=
  nhds_bot_basis

theorem nhds_zero_basis_Iic : (ğ“ (0 : â„â‰¥0âˆ)).HasBasis (fun a : â„â‰¥0âˆ => 0 < a) Iic :=
  nhds_bot_basis_Iic

-- TODO: add a TC for `â‰  âˆ`?
@[instance]
theorem nhdsGT_coe_neBot {r : â„â‰¥0} : (ğ“[>] (r : â„â‰¥0âˆ)).NeBot :=
  nhdsGT_neBot_of_exists_gt âŸ¨âˆ, ENNReal.coe_lt_topâŸ©

@[instance] theorem nhdsGT_zero_neBot : (ğ“[>] (0 : â„â‰¥0âˆ)).NeBot := nhdsGT_coe_neBot

@[instance] theorem nhdsGT_one_neBot : (ğ“[>] (1 : â„â‰¥0âˆ)).NeBot := nhdsGT_coe_neBot

@[instance] theorem nhdsGT_nat_neBot (n : â„•) : (ğ“[>] (n : â„â‰¥0âˆ)).NeBot := nhdsGT_coe_neBot

@[instance]
theorem nhdsGT_ofNat_neBot (n : â„•) [n.AtLeastTwo] : (ğ“[>] (OfNat.ofNat n : â„â‰¥0âˆ)).NeBot :=
  nhdsGT_coe_neBot

@[instance]
theorem nhdsLT_neBot [NeZero x] : (ğ“[<] x).NeBot :=
  nhdsWithin_Iio_self_neBot' âŸ¨0, NeZero.pos xâŸ©

/-- Closed intervals `Set.Icc (x - Îµ) (x + Îµ)`, `Îµ â‰  0`, form a basis of neighborhoods of an
extended nonnegative real number `x â‰  âˆ`. We use `Set.Icc` instead of `Set.Ioo` because this way the
statement works for `x = 0`.
-/
theorem hasBasis_nhds_of_ne_top' (xt : x â‰  âˆ) :
    (ğ“ x).HasBasis (Â· â‰  0) (fun Îµ => Icc (x - Îµ) (x + Îµ)) := by
  rcases (zero_le x).eq_or_lt with rfl | x0
  Â· simp_rw [zero_tsub, zero_add, â† bot_eq_zero, Icc_bot, â† bot_lt_iff_ne_bot]
    exact nhds_bot_basis_Iic
  Â· refine (nhds_basis_Ioo' âŸ¨_, x0âŸ© âŸ¨_, xt.lt_topâŸ©).to_hasBasis ?_ fun Îµ Îµ0 => ?_
    Â· rintro âŸ¨a, bâŸ© âŸ¨ha, hbâŸ©
      rcases exists_between (tsub_pos_of_lt ha) with âŸ¨Îµ, Îµ0, hÎµâŸ©
      rcases lt_iff_exists_add_pos_lt.1 hb with âŸ¨Î´, Î´0, hÎ´âŸ©
      refine âŸ¨min Îµ Î´, (lt_min Îµ0 (coe_pos.2 Î´0)).ne', Icc_subset_Ioo ?_ ?_âŸ©
      Â· exact lt_tsub_comm.2 ((min_le_left _ _).trans_lt hÎµ)
      Â· exact (add_le_add_left (min_le_right _ _) _).trans_lt hÎ´
    Â· exact âŸ¨(x - Îµ, x + Îµ), âŸ¨ENNReal.sub_lt_self xt x0.ne' Îµ0,
        lt_add_right xt Îµ0âŸ©, Ioo_subset_Icc_selfâŸ©

theorem hasBasis_nhds_of_ne_top (xt : x â‰  âˆ) :
    (ğ“ x).HasBasis (0 < Â·) (fun Îµ => Icc (x - Îµ) (x + Îµ)) := by
  simpa only [pos_iff_ne_zero] using hasBasis_nhds_of_ne_top' xt

theorem Icc_mem_nhds (xt : x â‰  âˆ) (Îµ0 : Îµ â‰  0) : Icc (x - Îµ) (x + Îµ) âˆˆ ğ“ x :=
  (hasBasis_nhds_of_ne_top' xt).mem_of_mem Îµ0

theorem nhds_of_ne_top (xt : x â‰  âˆ) : ğ“ x = â¨… Îµ > 0, ğ“Ÿ (Icc (x - Îµ) (x + Îµ)) :=
  (hasBasis_nhds_of_ne_top xt).eq_biInf

theorem biInf_le_nhds : âˆ€ x : â„â‰¥0âˆ, â¨… Îµ > 0, ğ“Ÿ (Icc (x - Îµ) (x + Îµ)) â‰¤ ğ“ x
  | âˆ => iInfâ‚‚_le_of_le 1 one_pos <| by
    simpa only [â† coe_one, top_sub_coe, top_add, Icc_self, principal_singleton] using pure_le_nhds _
  | (x : â„â‰¥0) => (nhds_of_ne_top coe_ne_top).ge

protected theorem tendsto_nhds_of_Icc {f : Filter Î±} {u : Î± â†’ â„â‰¥0âˆ} {a : â„â‰¥0âˆ}
    (h : âˆ€ Îµ > 0, âˆ€á¶  x in f, u x âˆˆ Icc (a - Îµ) (a + Îµ)) : Tendsto u f (ğ“ a) := by
  refine Tendsto.mono_right ?_ (biInf_le_nhds _)
  simpa only [tendsto_iInf, tendsto_principal]

/-- Characterization of neighborhoods for `â„â‰¥0âˆ` numbers. See also `tendsto_order`
for a version with strict inequalities. -/
protected theorem tendsto_nhds {f : Filter Î±} {u : Î± â†’ â„â‰¥0âˆ} {a : â„â‰¥0âˆ} (ha : a â‰  âˆ) :
    Tendsto u f (ğ“ a) â†” âˆ€ Îµ > 0, âˆ€á¶  x in f, u x âˆˆ Icc (a - Îµ) (a + Îµ) := by
  simp only [nhds_of_ne_top ha, tendsto_iInf, tendsto_principal]

protected theorem tendsto_nhds_zero {f : Filter Î±} {u : Î± â†’ â„â‰¥0âˆ} :
    Tendsto u f (ğ“ 0) â†” âˆ€ Îµ > 0, âˆ€á¶  x in f, u x â‰¤ Îµ :=
  nhds_zero_basis_Iic.tendsto_right_iff

theorem tendsto_const_sub_nhds_zero_iff {l : Filter Î±} {f : Î± â†’ â„â‰¥0âˆ} {a : â„â‰¥0âˆ} (ha : a â‰  âˆ)
    (hfa : âˆ€ n, f n â‰¤ a) :
    Tendsto (fun n â†¦ a - f n) l (ğ“ 0) â†” Tendsto (fun n â†¦ f n) l (ğ“ a) := by
  rw [ENNReal.tendsto_nhds_zero, ENNReal.tendsto_nhds ha]
  refine âŸ¨fun h Îµ hÎµ â†¦ ?_, fun h Îµ hÎµ â†¦ ?_âŸ©
  Â· filter_upwards [h Îµ hÎµ] with n hn
    refine âŸ¨?_, (hfa n).trans (le_add_right le_rfl)âŸ©
    rw [tsub_le_iff_right] at hn âŠ¢
    rwa [add_comm]
  Â· filter_upwards [h Îµ hÎµ] with n hn
    have hN_left := hn.1
    rw [tsub_le_iff_right] at hN_left âŠ¢
    rwa [add_comm]

protected theorem tendsto_atTop [Nonempty Î²] [SemilatticeSup Î²] {f : Î² â†’ â„â‰¥0âˆ} {a : â„â‰¥0âˆ}
    (ha : a â‰  âˆ) : Tendsto f atTop (ğ“ a) â†” âˆ€ Îµ > 0, âˆƒ N, âˆ€ n â‰¥ N, f n âˆˆ Icc (a - Îµ) (a + Îµ) :=
  .trans (atTop_basis.tendsto_iff (hasBasis_nhds_of_ne_top ha)) (by simp only [true_and]; rfl)

protected theorem tendsto_atTop_zero [Nonempty Î²] [SemilatticeSup Î²] {f : Î² â†’ â„â‰¥0âˆ} :
    Tendsto f atTop (ğ“ 0) â†” âˆ€ Îµ > 0, âˆƒ N, âˆ€ n â‰¥ N, f n â‰¤ Îµ :=
  .trans (atTop_basis.tendsto_iff nhds_zero_basis_Iic) (by simp only [true_and]; rfl)

theorem tendsto_atTop_zero_iff_le_of_antitone {Î² : Type*} [Nonempty Î²] [SemilatticeSup Î²]
    {f : Î² â†’ â„â‰¥0âˆ} (hf : Antitone f) :
    Filter.Tendsto f Filter.atTop (ğ“ 0) â†” âˆ€ Îµ, 0 < Îµ â†’ âˆƒ n : Î², f n â‰¤ Îµ := by
  rw [ENNReal.tendsto_atTop_zero]
  refine âŸ¨fun h â†¦ fun Îµ hÎµ â†¦ ?_, fun h â†¦ fun Îµ hÎµ â†¦ ?_âŸ©
  Â· obtain âŸ¨n, hnâŸ© := h Îµ hÎµ
    exact âŸ¨n, hn n le_rflâŸ©
  Â· obtain âŸ¨n, hnâŸ© := h Îµ hÎµ
    exact âŸ¨n, fun m hm â†¦ (hf hm).trans hnâŸ©

theorem tendsto_atTop_zero_iff_lt_of_antitone {Î² : Type*} [Nonempty Î²] [SemilatticeSup Î²]
    {f : Î² â†’ â„â‰¥0âˆ} (hf : Antitone f) :
    Filter.Tendsto f Filter.atTop (ğ“ 0) â†” âˆ€ Îµ, 0 < Îµ â†’ âˆƒ n : Î², f n < Îµ := by
  rw [ENNReal.tendsto_atTop_zero_iff_le_of_antitone hf]
  constructor <;> intro h Îµ hÎµ
  Â· obtain âŸ¨n, hnâŸ© := h (min 1 (Îµ / 2))
      (lt_min_iff.mpr âŸ¨zero_lt_one, (ENNReal.div_pos_iff.mpr âŸ¨ne_of_gt hÎµ, ENNReal.ofNat_ne_topâŸ©)âŸ©)
    Â· refine âŸ¨n, hn.trans_lt ?_âŸ©
      by_cases hÎµ_top : Îµ = âˆ
      Â· rw [hÎµ_top]
        exact (min_le_left _ _).trans_lt ENNReal.one_lt_top
      refine (min_le_right _ _).trans_lt ?_
      rw [ENNReal.div_lt_iff (Or.inr hÎµ.ne') (Or.inr hÎµ_top)]
      conv_lhs => rw [â† mul_one Îµ]
      rw [ENNReal.mul_lt_mul_left hÎµ.ne' hÎµ_top]
      norm_num
  Â· obtain âŸ¨n, hnâŸ© := h Îµ hÎµ
    exact âŸ¨n, hn.leâŸ©

theorem tendsto_sub : âˆ€ {a b : â„â‰¥0âˆ}, (a â‰  âˆ âˆ¨ b â‰  âˆ) â†’
    Tendsto (fun p : â„â‰¥0âˆ Ã— â„â‰¥0âˆ => p.1 - p.2) (ğ“ (a, b)) (ğ“ (a - b))
  | âˆ, âˆ, h => by simp only [ne_eq, not_true_eq_false, or_self] at h
  | âˆ, (b : â„â‰¥0), _ => by
    rw [top_sub_coe, tendsto_nhds_top_iff_nnreal]
    refine fun x => ((lt_mem_nhds <| @coe_lt_top (b + 1 + x)).prod_nhds
      (ge_mem_nhds <| coe_lt_coe.2 <| lt_add_one b)).mono fun y hy => ?_
    rw [lt_tsub_iff_left]
    calc y.2 + x â‰¤ â†‘(b + 1) + x := add_le_add_right hy.2 _
    _ < y.1 := hy.1
  | (a : â„â‰¥0), âˆ, _ => by
    rw [sub_top]
    refine (tendsto_pure.2 ?_).mono_right (pure_le_nhds _)
    exact ((gt_mem_nhds <| coe_lt_coe.2 <| lt_add_one a).prod_nhds
      (lt_mem_nhds <| @coe_lt_top (a + 1))).mono fun x hx =>
        tsub_eq_zero_iff_le.2 (hx.1.trans hx.2).le
  | (a : â„â‰¥0), (b : â„â‰¥0), _ => by
    simp only [nhds_coe_coe, tendsto_map'_iff, â† ENNReal.coe_sub, Function.comp_def, tendsto_coe]
    exact continuous_sub.tendsto (a, b)

protected theorem Tendsto.sub {f : Filter Î±} {ma : Î± â†’ â„â‰¥0âˆ} {mb : Î± â†’ â„â‰¥0âˆ} {a b : â„â‰¥0âˆ}
    (hma : Tendsto ma f (ğ“ a)) (hmb : Tendsto mb f (ğ“ b)) (h : a â‰  âˆ âˆ¨ b â‰  âˆ) :
    Tendsto (fun a => ma a - mb a) f (ğ“ (a - b)) :=
  show Tendsto ((fun p : â„â‰¥0âˆ Ã— â„â‰¥0âˆ => p.1 - p.2) âˆ˜ fun a => (ma a, mb a)) f (ğ“ (a - b)) from
    Tendsto.comp (ENNReal.tendsto_sub h) (hma.prodMk_nhds hmb)

protected theorem tendsto_mul (ha : a â‰  0 âˆ¨ b â‰  âˆ) (hb : b â‰  0 âˆ¨ a â‰  âˆ) :
    Tendsto (fun p : â„â‰¥0âˆ Ã— â„â‰¥0âˆ => p.1 * p.2) (ğ“ (a, b)) (ğ“ (a * b)) := by
  have ht : âˆ€ b : â„â‰¥0âˆ, b â‰  0 â†’
      Tendsto (fun p : â„â‰¥0âˆ Ã— â„â‰¥0âˆ => p.1 * p.2) (ğ“ (âˆ, b)) (ğ“ âˆ) := fun b hb => by
    refine tendsto_nhds_top_iff_nnreal.2 fun n => ?_
    rcases lt_iff_exists_nnreal_btwn.1 (pos_iff_ne_zero.2 hb) with âŸ¨Îµ, hÎµ, hÎµbâŸ©
    have : âˆ€á¶  c : â„â‰¥0âˆ Ã— â„â‰¥0âˆ in ğ“ (âˆ, b), â†‘n / â†‘Îµ < c.1 âˆ§ â†‘Îµ < c.2 :=
      (lt_mem_nhds <| div_lt_top coe_ne_top hÎµ.ne').prod_nhds (lt_mem_nhds hÎµb)
    refine this.mono fun c hc => ?_
    exact (ENNReal.div_mul_cancel hÎµ.ne' coe_ne_top).symm.trans_lt (mul_lt_mul hc.1 hc.2)
  induction a with
  | top => simp only [ne_eq, or_false, not_true_eq_false] at hb; simp [ht b hb, top_mul hb]
  | coe a =>
    induction b with
    | top =>
      simp only [ne_eq, or_false, not_true_eq_false] at ha
      simpa [Function.comp_def, mul_comm, mul_top ha]
        using (ht a ha).comp (continuous_swap.tendsto (ofNNReal a, âˆ))
    | coe b =>
      simp only [nhds_coe_coe, â† coe_mul, tendsto_coe, tendsto_map'_iff, Function.comp_def,
        tendsto_mul]

protected theorem Tendsto.mul {f : Filter Î±} {ma : Î± â†’ â„â‰¥0âˆ} {mb : Î± â†’ â„â‰¥0âˆ} {a b : â„â‰¥0âˆ}
    (hma : Tendsto ma f (ğ“ a)) (ha : a â‰  0 âˆ¨ b â‰  âˆ) (hmb : Tendsto mb f (ğ“ b))
    (hb : b â‰  0 âˆ¨ a â‰  âˆ) : Tendsto (fun a => ma a * mb a) f (ğ“ (a * b)) :=
  show Tendsto ((fun p : â„â‰¥0âˆ Ã— â„â‰¥0âˆ => p.1 * p.2) âˆ˜ fun a => (ma a, mb a)) f (ğ“ (a * b)) from
    Tendsto.comp (ENNReal.tendsto_mul ha hb) (hma.prodMk_nhds hmb)

theorem _root_.ContinuousOn.ennreal_mul [TopologicalSpace Î±] {f g : Î± â†’ â„â‰¥0âˆ} {s : Set Î±}
    (hf : ContinuousOn f s) (hg : ContinuousOn g s) (hâ‚ : âˆ€ x âˆˆ s, f x â‰  0 âˆ¨ g x â‰  âˆ)
    (hâ‚‚ : âˆ€ x âˆˆ s, g x â‰  0 âˆ¨ f x â‰  âˆ) : ContinuousOn (fun x => f x * g x) s := fun x hx =>
  ENNReal.Tendsto.mul (hf x hx) (hâ‚ x hx) (hg x hx) (hâ‚‚ x hx)

theorem _root_.Continuous.ennreal_mul [TopologicalSpace Î±] {f g : Î± â†’ â„â‰¥0âˆ} (hf : Continuous f)
    (hg : Continuous g) (hâ‚ : âˆ€ x, f x â‰  0 âˆ¨ g x â‰  âˆ) (hâ‚‚ : âˆ€ x, g x â‰  0 âˆ¨ f x â‰  âˆ) :
    Continuous fun x => f x * g x :=
  continuous_iff_continuousAt.2 fun x =>
    ENNReal.Tendsto.mul hf.continuousAt (hâ‚ x) hg.continuousAt (hâ‚‚ x)

protected theorem Tendsto.const_mul {f : Filter Î±} {m : Î± â†’ â„â‰¥0âˆ} {a b : â„â‰¥0âˆ}
    (hm : Tendsto m f (ğ“ b)) (hb : b â‰  0 âˆ¨ a â‰  âˆ) : Tendsto (fun b => a * m b) f (ğ“ (a * b)) :=
  by_cases (fun (this : a = 0) => by simp [this, tendsto_const_nhds]) fun ha : a â‰  0 =>
    ENNReal.Tendsto.mul tendsto_const_nhds (Or.inl ha) hm hb

protected theorem Tendsto.mul_const {f : Filter Î±} {m : Î± â†’ â„â‰¥0âˆ} {a b : â„â‰¥0âˆ}
    (hm : Tendsto m f (ğ“ a)) (ha : a â‰  0 âˆ¨ b â‰  âˆ) : Tendsto (fun x => m x * b) f (ğ“ (a * b)) := by
  simpa only [mul_comm] using ENNReal.Tendsto.const_mul hm ha

theorem tendsto_finset_prod_of_ne_top {Î¹ : Type*} {f : Î¹ â†’ Î± â†’ â„â‰¥0âˆ} {x : Filter Î±} {a : Î¹ â†’ â„â‰¥0âˆ}
    (s : Finset Î¹) (h : âˆ€ i âˆˆ s, Tendsto (f i) x (ğ“ (a i))) (h' : âˆ€ i âˆˆ s, a i â‰  âˆ) :
    Tendsto (fun b => âˆ c âˆˆ s, f c b) x (ğ“ (âˆ c âˆˆ s, a c)) := by
  classical
  induction s using Finset.induction with
  | empty => simp [tendsto_const_nhds]
  | insert _ _ has IH =>
    simp only [Finset.prod_insert has]
    apply Tendsto.mul (h _ (Finset.mem_insert_self _ _))
    Â· right
      exact prod_ne_top fun i hi => h' _ (Finset.mem_insert_of_mem hi)
    Â· exact IH (fun i hi => h _ (Finset.mem_insert_of_mem hi)) fun i hi =>
        h' _ (Finset.mem_insert_of_mem hi)
    Â· exact Or.inr (h' _ (Finset.mem_insert_self _ _))

protected theorem continuousAt_const_mul {a b : â„â‰¥0âˆ} (h : a â‰  âˆ âˆ¨ b â‰  0) :
    ContinuousAt (a * Â·) b :=
  Tendsto.const_mul tendsto_id h.symm

protected theorem continuousAt_mul_const {a b : â„â‰¥0âˆ} (h : a â‰  âˆ âˆ¨ b â‰  0) :
    ContinuousAt (fun x => x * a) b :=
  Tendsto.mul_const tendsto_id h.symm

@[fun_prop]
protected theorem continuous_const_mul {a : â„â‰¥0âˆ} (ha : a â‰  âˆ) : Continuous (a * Â·) :=
  continuous_iff_continuousAt.2 fun _ => ENNReal.continuousAt_const_mul (Or.inl ha)

@[fun_prop]
protected theorem continuous_mul_const {a : â„â‰¥0âˆ} (ha : a â‰  âˆ) : Continuous fun x => x * a :=
  continuous_iff_continuousAt.2 fun _ => ENNReal.continuousAt_mul_const (Or.inl ha)

@[fun_prop]
protected theorem continuous_div_const (c : â„â‰¥0âˆ) (c_ne_zero : c â‰  0) :
    Continuous fun x : â„â‰¥0âˆ => x / c :=
  ENNReal.continuous_mul_const <| ENNReal.inv_ne_top.2 c_ne_zero

@[continuity, fun_prop]
protected theorem continuous_pow (n : â„•) : Continuous fun a : â„â‰¥0âˆ => a ^ n := by
  induction n with
  | zero => simp [continuous_const]
  | succ n IH =>
    simp_rw [pow_add, pow_one, continuous_iff_continuousAt]
    intro x
    refine ENNReal.Tendsto.mul (IH.tendsto _) ?_ tendsto_id ?_ <;> by_cases H : x = 0
    Â· simp only [H, zero_ne_top, Ne, or_true, not_false_iff]
    Â· exact Or.inl fun h => H (pow_eq_zero h)
    Â· simp only [H, pow_eq_top_iff, zero_ne_top, false_or, not_true, Ne,
        not_false_iff, false_and]
    Â· simp only [H, true_or, Ne, not_false_iff]

theorem continuousOn_sub :
    ContinuousOn (fun p : â„â‰¥0âˆ Ã— â„â‰¥0âˆ => p.fst - p.snd) { p : â„â‰¥0âˆ Ã— â„â‰¥0âˆ | p â‰  âŸ¨âˆ, âˆâŸ© } := by
  rw [ContinuousOn]
  rintro âŸ¨x, yâŸ© hp
  simp only [Ne, Set.mem_setOf_eq, Prod.mk_inj] at hp
  exact tendsto_nhdsWithin_of_tendsto_nhds (tendsto_sub (not_and_or.mp hp))

theorem continuous_sub_left {a : â„â‰¥0âˆ} (a_ne_top : a â‰  âˆ) : Continuous (a - Â·) := by
  change Continuous (Function.uncurry Sub.sub âˆ˜ (a, Â·))
  refine continuousOn_sub.comp_continuous (.prodMk_right a) fun x => ?_
  simp only [a_ne_top, Ne, mem_setOf_eq, Prod.mk_inj, false_and, not_false_iff]

theorem continuous_nnreal_sub {a : â„â‰¥0} : Continuous fun x : â„â‰¥0âˆ => (a : â„â‰¥0âˆ) - x :=
  continuous_sub_left coe_ne_top

theorem continuousOn_sub_left (a : â„â‰¥0âˆ) : ContinuousOn (a - Â·) { x : â„â‰¥0âˆ | x â‰  âˆ } := by
  rw [show (fun x => a - x) = (fun p : â„â‰¥0âˆ Ã— â„â‰¥0âˆ => p.fst - p.snd) âˆ˜ fun x => âŸ¨a, xâŸ© by rfl]
  apply continuousOn_sub.comp (by fun_prop)
  rintro _ h (_ | _)
  exact h none_eq_top

theorem continuous_sub_right (a : â„â‰¥0âˆ) : Continuous fun x : â„â‰¥0âˆ => x - a := by
  by_cases a_infty : a = âˆ
  Â· simp [a_infty, continuous_const, tsub_eq_zero_of_le]
  Â· rw [show (fun x => x - a) = (fun p : â„â‰¥0âˆ Ã— â„â‰¥0âˆ => p.fst - p.snd) âˆ˜ fun x => âŸ¨x, aâŸ© by rfl]
    apply continuousOn_sub.comp_continuous (by fun_prop)
    intro x
    simp only [a_infty, Ne, mem_setOf_eq, Prod.mk_inj, and_false, not_false_iff]

protected theorem Tendsto.pow {f : Filter Î±} {m : Î± â†’ â„â‰¥0âˆ} {a : â„â‰¥0âˆ} {n : â„•}
    (hm : Tendsto m f (ğ“ a)) : Tendsto (fun x => m x ^ n) f (ğ“ (a ^ n)) :=
  ((ENNReal.continuous_pow n).tendsto a).comp hm

theorem le_of_forall_lt_one_mul_le {x y : â„â‰¥0âˆ} (h : âˆ€ a < 1, a * x â‰¤ y) : x â‰¤ y := by
  have : Tendsto (Â· * x) (ğ“[<] 1) (ğ“ (1 * x)) :=
    (ENNReal.continuousAt_mul_const (Or.inr one_ne_zero)).mono_left inf_le_left
  rw [one_mul] at this
  exact le_of_tendsto this (eventually_nhdsWithin_iff.2 <| Eventually.of_forall h)

theorem inv_limsup {Î¹ : Sort _} {x : Î¹ â†’ â„â‰¥0âˆ} {l : Filter Î¹} :
    (limsup x l)â»Â¹ = liminf (fun i => (x i)â»Â¹) l :=
  OrderIso.invENNReal.limsup_apply

theorem inv_liminf {Î¹ : Sort _} {x : Î¹ â†’ â„â‰¥0âˆ} {l : Filter Î¹} :
    (liminf x l)â»Â¹ = limsup (fun i => (x i)â»Â¹) l :=
  OrderIso.invENNReal.liminf_apply

@[fun_prop]
protected theorem continuous_zpow : âˆ€ n : â„¤, Continuous (Â· ^ n : â„â‰¥0âˆ â†’ â„â‰¥0âˆ)
  | (n : â„•) => mod_cast ENNReal.continuous_pow n
  | .negSucc n => by simpa using (ENNReal.continuous_pow _).inv

@[simp] -- TODO: generalize to `[InvolutiveInv _] [ContinuousInv _]`
protected theorem tendsto_inv_iff {f : Filter Î±} {m : Î± â†’ â„â‰¥0âˆ} {a : â„â‰¥0âˆ} :
    Tendsto (fun x => (m x)â»Â¹) f (ğ“ aâ»Â¹) â†” Tendsto m f (ğ“ a) :=
  âŸ¨fun h => by simpa only [inv_inv] using Tendsto.inv h, Tendsto.invâŸ©

protected theorem Tendsto.div {f : Filter Î±} {ma : Î± â†’ â„â‰¥0âˆ} {mb : Î± â†’ â„â‰¥0âˆ} {a b : â„â‰¥0âˆ}
    (hma : Tendsto ma f (ğ“ a)) (ha : a â‰  0 âˆ¨ b â‰  0) (hmb : Tendsto mb f (ğ“ b))
    (hb : b â‰  âˆ âˆ¨ a â‰  âˆ) : Tendsto (fun a => ma a / mb a) f (ğ“ (a / b)) := by
  apply Tendsto.mul hma _ (ENNReal.tendsto_inv_iff.2 hmb) _ <;> simp [ha, hb]

protected theorem Tendsto.const_div {f : Filter Î±} {m : Î± â†’ â„â‰¥0âˆ} {a b : â„â‰¥0âˆ}
    (hm : Tendsto m f (ğ“ b)) (hb : b â‰  âˆ âˆ¨ a â‰  âˆ) : Tendsto (fun b => a / m b) f (ğ“ (a / b)) := by
  apply Tendsto.const_mul (ENNReal.tendsto_inv_iff.2 hm)
  simp [hb]

protected theorem Tendsto.div_const {f : Filter Î±} {m : Î± â†’ â„â‰¥0âˆ} {a b : â„â‰¥0âˆ}
    (hm : Tendsto m f (ğ“ a)) (ha : a â‰  0 âˆ¨ b â‰  0) : Tendsto (fun x => m x / b) f (ğ“ (a / b)) := by
  apply Tendsto.mul_const hm
  simp [ha]

protected theorem tendsto_inv_nat_nhds_zero : Tendsto (fun n : â„• => (n : â„â‰¥0âˆ)â»Â¹) atTop (ğ“ 0) :=
  ENNReal.inv_top â–¸ ENNReal.tendsto_inv_iff.2 tendsto_nat_nhds_top

protected theorem tendsto_coe_sub {b : â„â‰¥0âˆ} :
    Tendsto (fun b : â„â‰¥0âˆ => â†‘r - b) (ğ“ b) (ğ“ (â†‘r - b)) :=
  continuous_nnreal_sub.tendsto _

theorem exists_countable_dense_no_zero_top :
    âˆƒ s : Set â„â‰¥0âˆ, s.Countable âˆ§ Dense s âˆ§ 0 âˆ‰ s âˆ§ âˆ âˆ‰ s := by
  obtain âŸ¨s, s_count, s_dense, hsâŸ© :
    âˆƒ s : Set â„â‰¥0âˆ, s.Countable âˆ§ Dense s âˆ§ (âˆ€ x, IsBot x â†’ x âˆ‰ s) âˆ§ âˆ€ x, IsTop x â†’ x âˆ‰ s :=
    exists_countable_dense_no_bot_top â„â‰¥0âˆ
  exact âŸ¨s, s_count, s_dense, fun h => hs.1 0 (by simp) h, fun h => hs.2 âˆ (by simp) hâŸ©

end TopologicalSpace

section Liminf

theorem exists_frequently_lt_of_liminf_ne_top {Î¹ : Type*} {l : Filter Î¹} {x : Î¹ â†’ â„}
    (hx : liminf (fun n => (Real.nnabs (x n) : â„â‰¥0âˆ)) l â‰  âˆ) : âˆƒ R, âˆƒá¶  n in l, x n < R := by
  by_contra h
  simp_rw [not_exists, not_frequently, not_lt] at h
  refine hx (ENNReal.eq_top_of_forall_nnreal_le fun r => le_limsInf_of_le (by isBoundedDefault) ?_)
  simp only [eventually_map, ENNReal.coe_le_coe]
  filter_upwards [h r] with i hi using hi.trans (le_abs_self (x i))

theorem exists_frequently_lt_of_liminf_ne_top' {Î¹ : Type*} {l : Filter Î¹} {x : Î¹ â†’ â„}
    (hx : liminf (fun n => (Real.nnabs (x n) : â„â‰¥0âˆ)) l â‰  âˆ) : âˆƒ R, âˆƒá¶  n in l, R < x n := by
  by_contra h
  simp_rw [not_exists, not_frequently, not_lt] at h
  refine hx (ENNReal.eq_top_of_forall_nnreal_le fun r => le_limsInf_of_le (by isBoundedDefault) ?_)
  simp only [eventually_map, ENNReal.coe_le_coe]
  filter_upwards [h (-r)] with i hi using(le_neg.1 hi).trans (neg_le_abs _)

theorem exists_upcrossings_of_not_bounded_under {Î¹ : Type*} {l : Filter Î¹} {x : Î¹ â†’ â„}
    (hf : liminf (fun i => (Real.nnabs (x i) : â„â‰¥0âˆ)) l â‰  âˆ)
    (hbdd : Â¬IsBoundedUnder (Â· â‰¤ Â·) l fun i => |x i|) :
    âˆƒ a b : â„š, a < b âˆ§ (âˆƒá¶  i in l, x i < a) âˆ§ âˆƒá¶  i in l, â†‘b < x i := by
  rw [isBoundedUnder_le_abs, not_and_or] at hbdd
  obtain hbdd | hbdd := hbdd
  Â· obtain âŸ¨R, hRâŸ© := exists_frequently_lt_of_liminf_ne_top hf
    obtain âŸ¨q, hqâŸ© := exists_rat_gt R
    refine âŸ¨q, q + 1, (lt_add_iff_pos_right _).2 zero_lt_one, ?_, ?_âŸ©
    Â· refine fun hcon => hR ?_
      filter_upwards [hcon] with x hx using not_lt.2 (lt_of_lt_of_le hq (not_lt.1 hx)).le
    Â· simp only [IsBoundedUnder, IsBounded, eventually_map, not_exists] at hbdd
      refine fun hcon => hbdd â†‘(q + 1) ?_
      filter_upwards [hcon] with x hx using not_lt.1 hx
  Â· obtain âŸ¨R, hRâŸ© := exists_frequently_lt_of_liminf_ne_top' hf
    obtain âŸ¨q, hqâŸ© := exists_rat_lt R
    refine âŸ¨q - 1, q, (sub_lt_self_iff _).2 zero_lt_one, ?_, ?_âŸ©
    Â· simp only [IsBoundedUnder, IsBounded, eventually_map, not_exists] at hbdd
      refine fun hcon => hbdd â†‘(q - 1) ?_
      filter_upwards [hcon] with x hx using not_lt.1 hx
    Â· refine fun hcon => hR ?_
      filter_upwards [hcon] with x hx using not_lt.2 ((not_lt.1 hx).trans hq.le)

end Liminf

section tsum

variable {f g : Î± â†’ â„â‰¥0âˆ}

@[norm_cast]
protected theorem hasSum_coe {f : Î± â†’ â„â‰¥0} {r : â„â‰¥0} :
    HasSum (fun a => (f a : â„â‰¥0âˆ)) â†‘r â†” HasSum f r := by
  simp only [HasSum, â† coe_finset_sum, tendsto_coe]

protected theorem tsum_coe_eq {f : Î± â†’ â„â‰¥0} (h : HasSum f r) : (âˆ‘' a, (f a : â„â‰¥0âˆ)) = r :=
  (ENNReal.hasSum_coe.2 h).tsum_eq

protected theorem coe_tsum {f : Î± â†’ â„â‰¥0} : Summable f â†’ â†‘(tsum f) = âˆ‘' a, (f a : â„â‰¥0âˆ)
  | âŸ¨r, hrâŸ© => by rw [hr.tsum_eq, ENNReal.tsum_coe_eq hr]

protected theorem hasSum : HasSum f (â¨† s : Finset Î±, âˆ‘ a âˆˆ s, f a) :=
  tendsto_atTop_iSup fun _ _ => Finset.sum_le_sum_of_subset

@[simp]
protected theorem summable : Summable f :=
  âŸ¨_, ENNReal.hasSumâŸ©

macro_rules | `(tactic| gcongr_discharger) => `(tactic| apply ENNReal.summable)

theorem tsum_coe_ne_top_iff_summable {f : Î² â†’ â„â‰¥0} : (âˆ‘' b, (f b : â„â‰¥0âˆ)) â‰  âˆ â†” Summable f := by
  refine âŸ¨fun h => ?_, fun h => ENNReal.coe_tsum h â–¸ ENNReal.coe_ne_topâŸ©
  lift âˆ‘' b, (f b : â„â‰¥0âˆ) to â„â‰¥0 using h with a ha
  refine âŸ¨a, ENNReal.hasSum_coe.1 ?_âŸ©
  rw [ha]
  exact ENNReal.summable.hasSum

protected theorem tsum_eq_iSup_sum : âˆ‘' a, f a = â¨† s : Finset Î±, âˆ‘ a âˆˆ s, f a :=
  ENNReal.hasSum.tsum_eq

protected theorem tsum_eq_iSup_sum' {Î¹ : Type*} (s : Î¹ â†’ Finset Î±) (hs : âˆ€ t, âˆƒ i, t âŠ† s i) :
    âˆ‘' a, f a = â¨† i, âˆ‘ a âˆˆ s i, f a := by
  rw [ENNReal.tsum_eq_iSup_sum]
  symm
  change â¨† i : Î¹, (fun t : Finset Î± => âˆ‘ a âˆˆ t, f a) (s i) = â¨† s : Finset Î±, âˆ‘ a âˆˆ s, f a
  exact (Finset.sum_mono_set f).iSup_comp_eq hs

protected theorem tsum_sigma {Î² : Î± â†’ Type*} (f : âˆ€ a, Î² a â†’ â„â‰¥0âˆ) :
    âˆ‘' p : Î£ a, Î² a, f p.1 p.2 = âˆ‘' (a) (b), f a b :=
  ENNReal.summable.tsum_sigma' fun _ => ENNReal.summable

protected theorem tsum_sigma' {Î² : Î± â†’ Type*} (f : (Î£ a, Î² a) â†’ â„â‰¥0âˆ) :
    âˆ‘' p : Î£ a, Î² a, f p = âˆ‘' (a) (b), f âŸ¨a, bâŸ© :=
  ENNReal.summable.tsum_sigma' fun _ => ENNReal.summable

protected theorem tsum_biUnion' {Î¹ : Type*} {S : Set Î¹} {f : Î± â†’ ENNReal} {t : Î¹ â†’ Set Î±}
    (h : S.PairwiseDisjoint t) : âˆ‘' x : â‹ƒ i âˆˆ S, t i, f x = âˆ‘' (i : S), âˆ‘' (x : t i), f x := by
  simp [â† ENNReal.tsum_sigma, â† (Set.biUnionEqSigmaOfDisjoint h).tsum_eq]

protected theorem tsum_biUnion {Î¹ : Type*} {f : Î± â†’ ENNReal} {t : Î¹ â†’ Set Î±}
    (h : Set.univ.PairwiseDisjoint t) : âˆ‘' x : â‹ƒ i, t i, f x = âˆ‘' (i) (x : t i), f x := by
  nth_rw 2 [â† tsum_univ]
  rw [â† ENNReal.tsum_biUnion' h, Set.biUnion_univ]

protected theorem tsum_prod {f : Î± â†’ Î² â†’ â„â‰¥0âˆ} : âˆ‘' p : Î± Ã— Î², f p.1 p.2 = âˆ‘' (a) (b), f a b :=
  ENNReal.summable.tsum_prod' fun _ => ENNReal.summable

protected theorem tsum_prod' {f : Î± Ã— Î² â†’ â„â‰¥0âˆ} : âˆ‘' p : Î± Ã— Î², f p = âˆ‘' (a) (b), f (a, b) :=
  ENNReal.summable.tsum_prod' fun _ => ENNReal.summable

protected theorem tsum_comm {f : Î± â†’ Î² â†’ â„â‰¥0âˆ} : âˆ‘' a, âˆ‘' b, f a b = âˆ‘' b, âˆ‘' a, f a b :=
  ENNReal.summable.tsum_comm' (fun _ => ENNReal.summable) fun _ => ENNReal.summable

protected theorem tsum_add : âˆ‘' a, (f a + g a) = âˆ‘' a, f a + âˆ‘' a, g a :=
  ENNReal.summable.tsum_add ENNReal.summable

protected theorem tsum_le_tsum (h : âˆ€ a, f a â‰¤ g a) : âˆ‘' a, f a â‰¤ âˆ‘' a, g a :=
  ENNReal.summable.tsum_le_tsum h ENNReal.summable

protected theorem sum_le_tsum {f : Î± â†’ â„â‰¥0âˆ} (s : Finset Î±) : âˆ‘ x âˆˆ s, f x â‰¤ âˆ‘' x, f x :=
  ENNReal.summable.sum_le_tsum s (fun _ _ => zero_le _)

protected theorem tsum_eq_iSup_nat' {f : â„• â†’ â„â‰¥0âˆ} {N : â„• â†’ â„•} (hN : Tendsto N atTop atTop) :
    âˆ‘' i : â„•, f i = â¨† i : â„•, âˆ‘ a âˆˆ Finset.range (N i), f a :=
  ENNReal.tsum_eq_iSup_sum' _ fun t =>
    let âŸ¨n, hnâŸ© := t.exists_nat_subset_range
    let âŸ¨k, _, hkâŸ© := exists_le_of_tendsto_atTop hN 0 n
    âŸ¨k, Finset.Subset.trans hn (Finset.range_mono hk)âŸ©

protected theorem tsum_eq_iSup_nat {f : â„• â†’ â„â‰¥0âˆ} :
    âˆ‘' i : â„•, f i = â¨† i : â„•, âˆ‘ a âˆˆ Finset.range i, f a :=
  ENNReal.tsum_eq_iSup_sum' _ Finset.exists_nat_subset_range

protected theorem tsum_eq_liminf_sum_nat {f : â„• â†’ â„â‰¥0âˆ} :
    âˆ‘' i, f i = liminf (fun n => âˆ‘ i âˆˆ Finset.range n, f i) atTop :=
  ENNReal.summable.hasSum.tendsto_sum_nat.liminf_eq.symm

protected theorem tsum_eq_limsup_sum_nat {f : â„• â†’ â„â‰¥0âˆ} :
    âˆ‘' i, f i = limsup (fun n => âˆ‘ i âˆˆ Finset.range n, f i) atTop :=
  ENNReal.summable.hasSum.tendsto_sum_nat.limsup_eq.symm

protected theorem le_tsum (a : Î±) : f a â‰¤ âˆ‘' a, f a :=
  ENNReal.summable.le_tsum' a

@[simp]
protected theorem tsum_eq_zero : âˆ‘' i, f i = 0 â†” âˆ€ i, f i = 0 :=
  ENNReal.summable.tsum_eq_zero_iff

protected theorem tsum_eq_top_of_eq_top : (âˆƒ a, f a = âˆ) â†’ âˆ‘' a, f a = âˆ
  | âŸ¨a, haâŸ© => top_unique <| ha â–¸ ENNReal.le_tsum a

protected theorem lt_top_of_tsum_ne_top {a : Î± â†’ â„â‰¥0âˆ} (tsum_ne_top : âˆ‘' i, a i â‰  âˆ) (j : Î±) :
    a j < âˆ := by
  contrapose! tsum_ne_top with h
  exact ENNReal.tsum_eq_top_of_eq_top âŸ¨j, top_unique hâŸ©

@[simp]
protected theorem tsum_top [Nonempty Î±] : âˆ‘' _ : Î±, âˆ = âˆ :=
  let âŸ¨aâŸ© := â€¹Nonempty Î±â€º
  ENNReal.tsum_eq_top_of_eq_top âŸ¨a, rflâŸ©

theorem tsum_const_eq_top_of_ne_zero {Î± : Type*} [Infinite Î±] {c : â„â‰¥0âˆ} (hc : c â‰  0) :
    âˆ‘' _ : Î±, c = âˆ := by
  have A : Tendsto (fun n : â„• => (n : â„â‰¥0âˆ) * c) atTop (ğ“ (âˆ * c)) := by
    apply ENNReal.Tendsto.mul_const tendsto_nat_nhds_top
    simp only [true_or, top_ne_zero, Ne, not_false_iff]
  have B : âˆ€ n : â„•, (n : â„â‰¥0âˆ) * c â‰¤ âˆ‘' _ : Î±, c := fun n => by
    rcases Infinite.exists_subset_card_eq Î± n with âŸ¨s, hsâŸ©
    simpa [hs] using @ENNReal.sum_le_tsum Î± (fun _ => c) s
  simpa [hc] using le_of_tendsto' A B

protected theorem ne_top_of_tsum_ne_top (h : âˆ‘' a, f a â‰  âˆ) (a : Î±) : f a â‰  âˆ := fun ha =>
  h <| ENNReal.tsum_eq_top_of_eq_top âŸ¨a, haâŸ©

protected theorem tsum_mul_left : âˆ‘' i, a * f i = a * âˆ‘' i, f i := by
  by_cases hf : âˆ€ i, f i = 0
  Â· simp [hf]
  Â· rw [â† ENNReal.tsum_eq_zero] at hf
    have : Tendsto (fun s : Finset Î± => âˆ‘ j âˆˆ s, a * f j) atTop (ğ“ (a * âˆ‘' i, f i)) := by
      simp only [â† Finset.mul_sum]
      exact ENNReal.Tendsto.const_mul ENNReal.summable.hasSum (Or.inl hf)
    exact HasSum.tsum_eq this

protected theorem tsum_mul_right : âˆ‘' i, f i * a = (âˆ‘' i, f i) * a := by
  simp [mul_comm, ENNReal.tsum_mul_left]

protected theorem tsum_const_smul {R} [SMul R â„â‰¥0âˆ] [IsScalarTower R â„â‰¥0âˆ â„â‰¥0âˆ] (a : R) :
    âˆ‘' i, a â€¢ f i = a â€¢ âˆ‘' i, f i := by
  simpa only [smul_one_mul] using @ENNReal.tsum_mul_left _ (a â€¢ (1 : â„â‰¥0âˆ)) _

@[simp]
theorem tsum_iSup_eq {Î± : Type*} (a : Î±) {f : Î± â†’ â„â‰¥0âˆ} : (âˆ‘' b : Î±, â¨† _ : a = b, f b) = f a :=
  (tsum_eq_single a fun _ h => by simp [h.symm]).trans <| by simp

theorem hasSum_iff_tendsto_nat {f : â„• â†’ â„â‰¥0âˆ} (r : â„â‰¥0âˆ) :
    HasSum f r â†” Tendsto (fun n : â„• => âˆ‘ i âˆˆ Finset.range n, f i) atTop (ğ“ r) := by
  refine âŸ¨HasSum.tendsto_sum_nat, fun h => ?_âŸ©
  rw [â† iSup_eq_of_tendsto _ h, â† ENNReal.tsum_eq_iSup_nat]
  Â· exact ENNReal.summable.hasSum
  Â· exact fun s t hst => Finset.sum_le_sum_of_subset (Finset.range_subset_range.2 hst)

theorem tendsto_nat_tsum (f : â„• â†’ â„â‰¥0âˆ) :
    Tendsto (fun n : â„• => âˆ‘ i âˆˆ Finset.range n, f i) atTop (ğ“ (âˆ‘' n, f n)) := by
  rw [â† hasSum_iff_tendsto_nat]
  exact ENNReal.summable.hasSum

theorem toNNReal_apply_of_tsum_ne_top {Î± : Type*} {f : Î± â†’ â„â‰¥0âˆ} (hf : âˆ‘' i, f i â‰  âˆ) (x : Î±) :
    (((ENNReal.toNNReal âˆ˜ f) x : â„â‰¥0) : â„â‰¥0âˆ) = f x :=
  coe_toNNReal <| ENNReal.ne_top_of_tsum_ne_top hf _

theorem summable_toNNReal_of_tsum_ne_top {Î± : Type*} {f : Î± â†’ â„â‰¥0âˆ} (hf : âˆ‘' i, f i â‰  âˆ) :
    Summable (ENNReal.toNNReal âˆ˜ f) := by
  simpa only [â† tsum_coe_ne_top_iff_summable, toNNReal_apply_of_tsum_ne_top hf] using hf

theorem tendsto_cofinite_zero_of_tsum_ne_top {Î±} {f : Î± â†’ â„â‰¥0âˆ} (hf : âˆ‘' x, f x â‰  âˆ) :
    Tendsto f cofinite (ğ“ 0) := by
  have f_ne_top : âˆ€ n, f n â‰  âˆ := ENNReal.ne_top_of_tsum_ne_top hf
  have h_f_coe : f = fun n => ((f n).toNNReal : ENNReal) :=
    funext fun n => (coe_toNNReal (f_ne_top n)).symm
  rw [h_f_coe, â† @coe_zero, tendsto_coe]
  exact NNReal.tendsto_cofinite_zero_of_summable (summable_toNNReal_of_tsum_ne_top hf)

theorem tendsto_atTop_zero_of_tsum_ne_top {f : â„• â†’ â„â‰¥0âˆ} (hf : âˆ‘' x, f x â‰  âˆ) :
    Tendsto f atTop (ğ“ 0) := by
  rw [â† Nat.cofinite_eq_atTop]
  exact tendsto_cofinite_zero_of_tsum_ne_top hf

/-- The sum over the complement of a finset tends to `0` when the finset grows to cover the whole
space. This does not need a summability assumption, as otherwise all sums are zero. -/
theorem tendsto_tsum_compl_atTop_zero {Î± : Type*} {f : Î± â†’ â„â‰¥0âˆ} (hf : âˆ‘' x, f x â‰  âˆ) :
    Tendsto (fun s : Finset Î± => âˆ‘' b : { x // x âˆ‰ s }, f b) atTop (ğ“ 0) := by
  lift f to Î± â†’ â„â‰¥0 using ENNReal.ne_top_of_tsum_ne_top hf
  convert ENNReal.tendsto_coe.2 (NNReal.tendsto_tsum_compl_atTop_zero f)
  rw [ENNReal.coe_tsum]
  exact NNReal.summable_comp_injective (tsum_coe_ne_top_iff_summable.1 hf) Subtype.coe_injective

protected theorem tsum_apply {Î¹ Î± : Type*} {f : Î¹ â†’ Î± â†’ â„â‰¥0âˆ} {x : Î±} :
    (âˆ‘' i, f i) x = âˆ‘' i, f i x :=
  tsum_apply <| Pi.summable.mpr fun _ => ENNReal.summable

theorem tsum_sub {f : â„• â†’ â„â‰¥0âˆ} {g : â„• â†’ â„â‰¥0âˆ} (hâ‚ : âˆ‘' i, g i â‰  âˆ) (hâ‚‚ : g â‰¤ f) :
    âˆ‘' i, (f i - g i) = âˆ‘' i, f i - âˆ‘' i, g i :=
  have : âˆ€ i, f i - g i + g i = f i := fun i => tsub_add_cancel_of_le (hâ‚‚ i)
  ENNReal.eq_sub_of_add_eq hâ‚ <| by simp only [â† ENNReal.tsum_add, this]

theorem tsum_comp_le_tsum_of_injective {f : Î± â†’ Î²} (hf : Injective f) (g : Î² â†’ â„â‰¥0âˆ) :
    âˆ‘' x, g (f x) â‰¤ âˆ‘' y, g y :=
  ENNReal.summable.tsum_le_tsum_of_inj f hf (fun _ _ => zero_le _) (fun _ => le_rfl)
    ENNReal.summable

theorem tsum_le_tsum_comp_of_surjective {f : Î± â†’ Î²} (hf : Surjective f) (g : Î² â†’ â„â‰¥0âˆ) :
    âˆ‘' y, g y â‰¤ âˆ‘' x, g (f x) :=
  calc âˆ‘' y, g y = âˆ‘' y, g (f (surjInv hf y)) := by simp only [surjInv_eq hf]
  _ â‰¤ âˆ‘' x, g (f x) := tsum_comp_le_tsum_of_injective (injective_surjInv hf) _

theorem tsum_mono_subtype (f : Î± â†’ â„â‰¥0âˆ) {s t : Set Î±} (h : s âŠ† t) :
    âˆ‘' x : s, f x â‰¤ âˆ‘' x : t, f x :=
  tsum_comp_le_tsum_of_injective (inclusion_injective h) _

theorem tsum_iUnion_le_tsum {Î¹ : Type*} (f : Î± â†’ â„â‰¥0âˆ) (t : Î¹ â†’ Set Î±) :
    âˆ‘' x : â‹ƒ i, t i, f x â‰¤ âˆ‘' i, âˆ‘' x : t i, f x :=
  calc âˆ‘' x : â‹ƒ i, t i, f x â‰¤ âˆ‘' x : Î£ i, t i, f x.2 :=
    tsum_le_tsum_comp_of_surjective (sigmaToiUnion_surjective t) _
  _ = âˆ‘' i, âˆ‘' x : t i, f x := ENNReal.tsum_sigma' _

theorem tsum_biUnion_le_tsum {Î¹ : Type*} (f : Î± â†’ â„â‰¥0âˆ) (s : Set Î¹) (t : Î¹ â†’ Set Î±) :
    âˆ‘' x : â‹ƒ i âˆˆ s, t i, f x â‰¤ âˆ‘' i : s, âˆ‘' x : t i, f x :=
  calc âˆ‘' x : â‹ƒ i âˆˆ s, t i, f x = âˆ‘' x : â‹ƒ i : s, t i, f x := tsum_congr_set_coe _ <| by simp
  _ â‰¤ âˆ‘' i : s, âˆ‘' x : t i, f x := tsum_iUnion_le_tsum _ _

theorem tsum_biUnion_le {Î¹ : Type*} (f : Î± â†’ â„â‰¥0âˆ) (s : Finset Î¹) (t : Î¹ â†’ Set Î±) :
    âˆ‘' x : â‹ƒ i âˆˆ s, t i, f x â‰¤ âˆ‘ i âˆˆ s, âˆ‘' x : t i, f x :=
  (tsum_biUnion_le_tsum f s.toSet t).trans_eq (Finset.tsum_subtype s fun i => âˆ‘' x : t i, f x)

theorem tsum_iUnion_le {Î¹ : Type*} [Fintype Î¹] (f : Î± â†’ â„â‰¥0âˆ) (t : Î¹ â†’ Set Î±) :
    âˆ‘' x : â‹ƒ i, t i, f x â‰¤ âˆ‘ i, âˆ‘' x : t i, f x := by
  rw [â† tsum_fintype]
  exact tsum_iUnion_le_tsum f t

theorem tsum_union_le (f : Î± â†’ â„â‰¥0âˆ) (s t : Set Î±) :
    âˆ‘' x : â†‘(s âˆª t), f x â‰¤ âˆ‘' x : s, f x + âˆ‘' x : t, f x :=
  calc âˆ‘' x : â†‘(s âˆª t), f x = âˆ‘' x : â‹ƒ b, cond b s t, f x := tsum_congr_set_coe _ union_eq_iUnion
  _ â‰¤ _ := by simpa using tsum_iUnion_le f (cond Â· s t)

open Classical in
theorem tsum_eq_add_tsum_ite {f : Î² â†’ â„â‰¥0âˆ} (b : Î²) :
    âˆ‘' x, f x = f b + âˆ‘' x, ite (x = b) 0 (f x) :=
  ENNReal.summable.tsum_eq_add_tsum_ite' b

theorem tsum_add_one_eq_top {f : â„• â†’ â„â‰¥0âˆ} (hf : âˆ‘' n, f n = âˆ) (hf0 : f 0 â‰  âˆ) :
    âˆ‘' n, f (n + 1) = âˆ := by
  rw [tsum_eq_zero_add' ENNReal.summable, add_eq_top] at hf
  exact hf.resolve_left hf0

/-- A sum of extended nonnegative reals which is finite can have only finitely many terms
above any positive threshold. -/
theorem finite_const_le_of_tsum_ne_top {Î¹ : Type*} {a : Î¹ â†’ â„â‰¥0âˆ} (tsum_ne_top : âˆ‘' i, a i â‰  âˆ)
    {Îµ : â„â‰¥0âˆ} (Îµ_ne_zero : Îµ â‰  0) : { i : Î¹ | Îµ â‰¤ a i }.Finite := by
  by_contra h
  have := Infinite.to_subtype h
  refine tsum_ne_top (top_unique ?_)
  calc âˆ = âˆ‘' _ : { i | Îµ â‰¤ a i }, Îµ := (tsum_const_eq_top_of_ne_zero Îµ_ne_zero).symm
  _ â‰¤ âˆ‘' i, a i := ENNReal.summable.tsum_le_tsum_of_inj (â†‘)
    Subtype.val_injective (fun _ _ => zero_le _) (fun i => i.2) ENNReal.summable

/-- Markov's inequality for `Finset.card` and `tsum` in `â„â‰¥0âˆ`. -/
theorem finset_card_const_le_le_of_tsum_le {Î¹ : Type*} {a : Î¹ â†’ â„â‰¥0âˆ} {c : â„â‰¥0âˆ} (c_ne_top : c â‰  âˆ)
    (tsum_le_c : âˆ‘' i, a i â‰¤ c) {Îµ : â„â‰¥0âˆ} (Îµ_ne_zero : Îµ â‰  0) :
    âˆƒ hf : { i : Î¹ | Îµ â‰¤ a i }.Finite, #hf.toFinset â‰¤ c / Îµ := by
  have hf : { i : Î¹ | Îµ â‰¤ a i }.Finite :=
    finite_const_le_of_tsum_ne_top (ne_top_of_le_ne_top c_ne_top tsum_le_c) Îµ_ne_zero
  refine âŸ¨hf, (ENNReal.le_div_iff_mul_le (.inl Îµ_ne_zero) (.inr c_ne_top)).2 ?_âŸ©
  calc #hf.toFinset * Îµ = âˆ‘ _i âˆˆ hf.toFinset, Îµ := by rw [Finset.sum_const, nsmul_eq_mul]
    _ â‰¤ âˆ‘ i âˆˆ hf.toFinset, a i := Finset.sum_le_sum fun i => hf.mem_toFinset.1
    _ â‰¤ âˆ‘' i, a i := ENNReal.sum_le_tsum _
    _ â‰¤ c := tsum_le_c

theorem tsum_fiberwise (f : Î² â†’ â„â‰¥0âˆ) (g : Î² â†’ Î³) :
    âˆ‘' x, âˆ‘' b : g â»Â¹' {x}, f b = âˆ‘' i, f i := by
  apply HasSum.tsum_eq
  let equiv := Equiv.sigmaFiberEquiv g
  apply (equiv.hasSum_iff.mpr ENNReal.summable.hasSum).sigma
  exact fun _ â†¦ ENNReal.summable.hasSum_iff.mpr rfl

end tsum

theorem tendsto_toReal_iff {Î¹} {fi : Filter Î¹} {f : Î¹ â†’ â„â‰¥0âˆ} (hf : âˆ€ i, f i â‰  âˆ) {x : â„â‰¥0âˆ}
    (hx : x â‰  âˆ) : Tendsto (fun n => (f n).toReal) fi (ğ“ x.toReal) â†” Tendsto f fi (ğ“ x) := by
  lift f to Î¹ â†’ â„â‰¥0 using hf
  lift x to â„â‰¥0 using hx
  simp [tendsto_coe]

theorem tsum_coe_ne_top_iff_summable_coe {f : Î± â†’ â„â‰¥0} :
    (âˆ‘' a, (f a : â„â‰¥0âˆ)) â‰  âˆ â†” Summable fun a => (f a : â„) := by
  rw [NNReal.summable_coe]
  exact tsum_coe_ne_top_iff_summable

theorem tsum_coe_eq_top_iff_not_summable_coe {f : Î± â†’ â„â‰¥0} :
    (âˆ‘' a, (f a : â„â‰¥0âˆ)) = âˆ â†” Â¬Summable fun a => (f a : â„) :=
  tsum_coe_ne_top_iff_summable_coe.not_right

theorem hasSum_toReal {f : Î± â†’ â„â‰¥0âˆ} (hsum : âˆ‘' x, f x â‰  âˆ) :
    HasSum (fun x => (f x).toReal) (âˆ‘' x, (f x).toReal) := by
  lift f to Î± â†’ â„â‰¥0 using ENNReal.ne_top_of_tsum_ne_top hsum
  simp only [coe_toReal, â† NNReal.coe_tsum, NNReal.hasSum_coe]
  exact (tsum_coe_ne_top_iff_summable.1 hsum).hasSum

theorem summable_toReal {f : Î± â†’ â„â‰¥0âˆ} (hsum : âˆ‘' x, f x â‰  âˆ) : Summable fun x => (f x).toReal :=
  (hasSum_toReal hsum).summable

end ENNReal

namespace NNReal

theorem tsum_eq_toNNReal_tsum {f : Î² â†’ â„â‰¥0} : âˆ‘' b, f b = (âˆ‘' b, (f b : â„â‰¥0âˆ)).toNNReal := by
  by_cases h : Summable f
  Â· rw [â† ENNReal.coe_tsum h, ENNReal.toNNReal_coe]
  Â· have A := tsum_eq_zero_of_not_summable h
    simp only [â† ENNReal.tsum_coe_ne_top_iff_summable, Classical.not_not] at h
    simp only [h, ENNReal.toNNReal_top, A]

/-- Comparison test of convergence of `â„â‰¥0`-valued series. -/
theorem exists_le_hasSum_of_le {f g : Î² â†’ â„â‰¥0} {r : â„â‰¥0} (hgf : âˆ€ b, g b â‰¤ f b) (hfr : HasSum f r) :
    âˆƒ p â‰¤ r, HasSum g p :=
  have : (âˆ‘' b, (g b : â„â‰¥0âˆ)) â‰¤ r := by
    refine hasSum_le (fun b => ?_) ENNReal.summable.hasSum (ENNReal.hasSum_coe.2 hfr)
    exact ENNReal.coe_le_coe.2 (hgf _)
  let âŸ¨p, Eq, hprâŸ© := ENNReal.le_coe_iff.1 this
  âŸ¨p, hpr, ENNReal.hasSum_coe.1 <| Eq â–¸ ENNReal.summable.hasSumâŸ©

/-- Comparison test of convergence of `â„â‰¥0`-valued series. -/
theorem summable_of_le {f g : Î² â†’ â„â‰¥0} (hgf : âˆ€ b, g b â‰¤ f b) : Summable f â†’ Summable g
  | âŸ¨_r, hfrâŸ© =>
    let âŸ¨_p, _, hpâŸ© := exists_le_hasSum_of_le hgf hfr
    hp.summable

/-- Summable non-negative functions have countable support -/
theorem _root_.Summable.countable_support_nnreal (f : Î± â†’ â„â‰¥0) (h : Summable f) :
    f.support.Countable := by
  rw [â† NNReal.summable_coe] at h
  simpa [support] using h.countable_support

/-- A series of non-negative real numbers converges to `r` in the sense of `HasSum` if and only if
the sequence of partial sum converges to `r`. -/
theorem hasSum_iff_tendsto_nat {f : â„• â†’ â„â‰¥0} {r : â„â‰¥0} :
    HasSum f r â†” Tendsto (fun n : â„• => âˆ‘ i âˆˆ Finset.range n, f i) atTop (ğ“ r) := by
  rw [â† ENNReal.hasSum_coe, ENNReal.hasSum_iff_tendsto_nat]
  simp only [â† ENNReal.coe_finset_sum]
  exact ENNReal.tendsto_coe

theorem not_summable_iff_tendsto_nat_atTop {f : â„• â†’ â„â‰¥0} :
    Â¬Summable f â†” Tendsto (fun n : â„• => âˆ‘ i âˆˆ Finset.range n, f i) atTop atTop := by
  constructor
  Â· intro h
    refine ((tendsto_of_monotone ?_).resolve_right h).comp ?_
    exacts [Finset.sum_mono_set _, tendsto_finset_range]
  Â· rintro hnat âŸ¨r, hrâŸ©
    exact not_tendsto_nhds_of_tendsto_atTop hnat _ (hasSum_iff_tendsto_nat.1 hr)

theorem summable_iff_not_tendsto_nat_atTop {f : â„• â†’ â„â‰¥0} :
    Summable f â†” Â¬Tendsto (fun n : â„• => âˆ‘ i âˆˆ Finset.range n, f i) atTop atTop := by
  rw [â† not_iff_not, Classical.not_not, not_summable_iff_tendsto_nat_atTop]

theorem summable_of_sum_range_le {f : â„• â†’ â„â‰¥0} {c : â„â‰¥0}
    (h : âˆ€ n, âˆ‘ i âˆˆ Finset.range n, f i â‰¤ c) : Summable f := by
  refine summable_iff_not_tendsto_nat_atTop.2 fun H => ?_
  rcases exists_lt_of_tendsto_atTop H 0 c with âŸ¨n, -, hnâŸ©
  exact lt_irrefl _ (hn.trans_le (h n))

theorem tsum_le_of_sum_range_le {f : â„• â†’ â„â‰¥0} {c : â„â‰¥0}
    (h : âˆ€ n, âˆ‘ i âˆˆ Finset.range n, f i â‰¤ c) : âˆ‘' n, f n â‰¤ c :=
  (summable_of_sum_range_le h).tsum_le_of_sum_range_le h

theorem tsum_comp_le_tsum_of_inj {Î² : Type*} {f : Î± â†’ â„â‰¥0} (hf : Summable f) {i : Î² â†’ Î±}
    (hi : Function.Injective i) : (âˆ‘' x, f (i x)) â‰¤ âˆ‘' x, f x :=
  (summable_comp_injective hf hi).tsum_le_tsum_of_inj i hi (fun _ _ => zero_le _) (fun _ => le_rfl)
    hf

theorem summable_sigma {Î² : Î± â†’ Type*} {f : (Î£ x, Î² x) â†’ â„â‰¥0} :
    Summable f â†” (âˆ€ x, Summable fun y => f âŸ¨x, yâŸ©) âˆ§ Summable fun x => âˆ‘' y, f âŸ¨x, yâŸ© := by
  constructor
  Â· simp only [â† NNReal.summable_coe, NNReal.coe_tsum]
    exact fun h => âŸ¨h.sigma_factor, h.sigmaâŸ©
  Â· rintro âŸ¨hâ‚, hâ‚‚âŸ©
    simpa only [â† ENNReal.tsum_coe_ne_top_iff_summable, ENNReal.tsum_sigma',
      ENNReal.coe_tsum (hâ‚ _)] using hâ‚‚

theorem indicator_summable {f : Î± â†’ â„â‰¥0} (hf : Summable f) (s : Set Î±) :
    Summable (s.indicator f) := by
  classical
  refine NNReal.summable_of_le (fun a => le_trans (le_of_eq (s.indicator_apply f a)) ?_) hf
  split_ifs
  Â· exact le_refl (f a)
  Â· exact zero_le_coe

theorem tsum_indicator_ne_zero {f : Î± â†’ â„â‰¥0} (hf : Summable f) {s : Set Î±} (h : âˆƒ a âˆˆ s, f a â‰  0) :
    (âˆ‘' x, (s.indicator f) x) â‰  0 := fun h' =>
  let âŸ¨a, ha, hapâŸ© := h
  hap ((Set.indicator_apply_eq_self.mpr (absurd ha)).symm.trans
    ((indicator_summable hf s).tsum_eq_zero_iff.1 h' a))

open Finset

/-- For `f : â„• â†’ â„â‰¥0`, then `âˆ‘' k, f (k + i)` tends to zero. This does not require a summability
assumption on `f`, as otherwise all sums are zero. -/
theorem tendsto_sum_nat_add (f : â„• â†’ â„â‰¥0) : Tendsto (fun i => âˆ‘' k, f (k + i)) atTop (ğ“ 0) := by
  rw [â† tendsto_coe]
  convert _root_.tendsto_sum_nat_add fun i => (f i : â„)
  norm_cast

nonrec theorem hasSum_lt {f g : Î± â†’ â„â‰¥0} {sf sg : â„â‰¥0} {i : Î±} (h : âˆ€ a : Î±, f a â‰¤ g a)
    (hi : f i < g i) (hf : HasSum f sf) (hg : HasSum g sg) : sf < sg := by
  have A : âˆ€ a : Î±, (f a : â„) â‰¤ g a := fun a => NNReal.coe_le_coe.2 (h a)
  have : (sf : â„) < sg := hasSum_lt A (NNReal.coe_lt_coe.2 hi) (hasSum_coe.2 hf) (hasSum_coe.2 hg)
  exact NNReal.coe_lt_coe.1 this

@[mono]
theorem hasSum_strict_mono {f g : Î± â†’ â„â‰¥0} {sf sg : â„â‰¥0} (hf : HasSum f sf) (hg : HasSum g sg)
    (h : f < g) : sf < sg :=
  let âŸ¨hle, _i, hiâŸ© := Pi.lt_def.mp h
  hasSum_lt hle hi hf hg

theorem tsum_lt_tsum {f g : Î± â†’ â„â‰¥0} {i : Î±} (h : âˆ€ a : Î±, f a â‰¤ g a) (hi : f i < g i)
    (hg : Summable g) : âˆ‘' n, f n < âˆ‘' n, g n :=
  hasSum_lt h hi (summable_of_le h hg).hasSum hg.hasSum

@[mono]
theorem tsum_strict_mono {f g : Î± â†’ â„â‰¥0} (hg : Summable g) (h : f < g) : âˆ‘' n, f n < âˆ‘' n, g n :=
  let âŸ¨hle, _i, hiâŸ© := Pi.lt_def.mp h
  tsum_lt_tsum hle hi hg

theorem tsum_pos {g : Î± â†’ â„â‰¥0} (hg : Summable g) (i : Î±) (hi : 0 < g i) : 0 < âˆ‘' b, g b := by
  rw [â† tsum_zero]
  exact tsum_lt_tsum (fun a => zero_le _) hi hg

open Classical in
theorem tsum_eq_add_tsum_ite {f : Î± â†’ â„â‰¥0} (hf : Summable f) (i : Î±) :
    âˆ‘' x, f x = f i + âˆ‘' x, ite (x = i) 0 (f x) := by
  refine (NNReal.summable_of_le (fun i' => ?_) hf).tsum_eq_add_tsum_ite' i
  rw [Function.update_apply]
  split_ifs <;> simp only [zero_le', le_rfl]

end NNReal

namespace ENNReal

theorem tsum_toNNReal_eq {f : Î± â†’ â„â‰¥0âˆ} (hf : âˆ€ a, f a â‰  âˆ) :
    (âˆ‘' a, f a).toNNReal = âˆ‘' a, (f a).toNNReal :=
  (congr_arg ENNReal.toNNReal (tsum_congr fun x => (coe_toNNReal (hf x)).symm)).trans
    NNReal.tsum_eq_toNNReal_tsum.symm

theorem tsum_toReal_eq {f : Î± â†’ â„â‰¥0âˆ} (hf : âˆ€ a, f a â‰  âˆ) :
    (âˆ‘' a, f a).toReal = âˆ‘' a, (f a).toReal := by
  simp only [ENNReal.toReal, tsum_toNNReal_eq hf, NNReal.coe_tsum]

theorem tendsto_sum_nat_add (f : â„• â†’ â„â‰¥0âˆ) (hf : âˆ‘' i, f i â‰  âˆ) :
    Tendsto (fun i => âˆ‘' k, f (k + i)) atTop (ğ“ 0) := by
  lift f to â„• â†’ â„â‰¥0 using ENNReal.ne_top_of_tsum_ne_top hf
  replace hf : Summable f := tsum_coe_ne_top_iff_summable.1 hf
  simp only [â† ENNReal.coe_tsum, NNReal.summable_nat_add _ hf, â† ENNReal.coe_zero]
  exact mod_cast NNReal.tendsto_sum_nat_add f

theorem tsum_le_of_sum_range_le {f : â„• â†’ â„â‰¥0âˆ} {c : â„â‰¥0âˆ}
    (h : âˆ€ n, âˆ‘ i âˆˆ Finset.range n, f i â‰¤ c) : âˆ‘' n, f n â‰¤ c :=
  ENNReal.summable.tsum_le_of_sum_range_le h

theorem hasSum_lt {f g : Î± â†’ â„â‰¥0âˆ} {sf sg : â„â‰¥0âˆ} {i : Î±} (h : âˆ€ a : Î±, f a â‰¤ g a) (hi : f i < g i)
    (hsf : sf â‰  âˆ) (hf : HasSum f sf) (hg : HasSum g sg) : sf < sg := by
  by_cases hsg : sg = âˆ
  Â· exact hsg.symm â–¸ lt_of_le_of_ne le_top hsf
  Â· have hg' : âˆ€ x, g x â‰  âˆ := ENNReal.ne_top_of_tsum_ne_top (hg.tsum_eq.symm â–¸ hsg)
    lift f to Î± â†’ â„â‰¥0 using fun x =>
      ne_of_lt (lt_of_le_of_lt (h x) <| lt_of_le_of_ne le_top (hg' x))
    lift g to Î± â†’ â„â‰¥0 using hg'
    lift sf to â„â‰¥0 using hsf
    lift sg to â„â‰¥0 using hsg
    simp only [coe_le_coe, coe_lt_coe] at h hi âŠ¢
    exact NNReal.hasSum_lt h hi (ENNReal.hasSum_coe.1 hf) (ENNReal.hasSum_coe.1 hg)

theorem tsum_lt_tsum {f g : Î± â†’ â„â‰¥0âˆ} {i : Î±} (hfi : tsum f â‰  âˆ) (h : âˆ€ a : Î±, f a â‰¤ g a)
    (hi : f i < g i) : âˆ‘' x, f x < âˆ‘' x, g x :=
  hasSum_lt h hi hfi ENNReal.summable.hasSum ENNReal.summable.hasSum

end ENNReal

theorem tsum_comp_le_tsum_of_inj {Î² : Type*} {f : Î± â†’ â„} (hf : Summable f) (hn : âˆ€ a, 0 â‰¤ f a)
    {i : Î² â†’ Î±} (hi : Function.Injective i) : tsum (f âˆ˜ i) â‰¤ tsum f := by
  lift f to Î± â†’ â„â‰¥0 using hn
  rw [NNReal.summable_coe] at hf
  simpa only [Function.comp_def, â† NNReal.coe_tsum] using NNReal.tsum_comp_le_tsum_of_inj hf hi

/-- Comparison test of convergence of series of non-negative real numbers. -/
theorem Summable.of_nonneg_of_le {f g : Î² â†’ â„} (hg : âˆ€ b, 0 â‰¤ g b) (hgf : âˆ€ b, g b â‰¤ f b)
    (hf : Summable f) : Summable g := by
  lift f to Î² â†’ â„â‰¥0 using fun b => (hg b).trans (hgf b)
  lift g to Î² â†’ â„â‰¥0 using hg
  rw [NNReal.summable_coe] at hf âŠ¢
  exact NNReal.summable_of_le (fun b => NNReal.coe_le_coe.1 (hgf b)) hf

theorem Summable.toNNReal {f : Î± â†’ â„} (hf : Summable f) : Summable fun n => (f n).toNNReal := by
  apply NNReal.summable_coe.1
  refine .of_nonneg_of_le (fun n => NNReal.coe_nonneg _) (fun n => ?_) hf.abs
  simp only [le_abs_self, Real.coe_toNNReal', max_le_iff, abs_nonneg, and_self_iff]

lemma Summable.tsum_ofReal_lt_top {f : Î± â†’ â„} (hf : Summable f) : âˆ‘' i, .ofReal (f i) < âˆ := by
  unfold ENNReal.ofReal
  rw [lt_top_iff_ne_top, ENNReal.tsum_coe_ne_top_iff_summable]
  exact hf.toNNReal

lemma Summable.tsum_ofReal_ne_top {f : Î± â†’ â„} (hf : Summable f) : âˆ‘' i, .ofReal (f i) â‰  âˆ :=
  hf.tsum_ofReal_lt_top.ne

/-- Finitely summable non-negative functions have countable support -/
theorem _root_.Summable.countable_support_ennreal {f : Î± â†’ â„â‰¥0âˆ} (h : âˆ‘' (i : Î±), f i â‰  âˆ) :
    f.support.Countable := by
  lift f to Î± â†’ â„â‰¥0 using ENNReal.ne_top_of_tsum_ne_top h
  simpa [support] using (ENNReal.tsum_coe_ne_top_iff_summable.1 h).countable_support_nnreal

/-- A series of non-negative real numbers converges to `r` in the sense of `HasSum` if and only if
the sequence of partial sum converges to `r`. -/
theorem hasSum_iff_tendsto_nat_of_nonneg {f : â„• â†’ â„} (hf : âˆ€ i, 0 â‰¤ f i) (r : â„) :
    HasSum f r â†” Tendsto (fun n : â„• => âˆ‘ i âˆˆ Finset.range n, f i) atTop (ğ“ r) := by
  lift f to â„• â†’ â„â‰¥0 using hf
  simp only [HasSum, â† NNReal.coe_sum, NNReal.tendsto_coe']
  exact exists_congr fun hr => NNReal.hasSum_iff_tendsto_nat

theorem ENNReal.ofReal_tsum_of_nonneg {f : Î± â†’ â„} (hf_nonneg : âˆ€ n, 0 â‰¤ f n) (hf : Summable f) :
    ENNReal.ofReal (âˆ‘' n, f n) = âˆ‘' n, ENNReal.ofReal (f n) := by
  simp_rw [ENNReal.ofReal, ENNReal.tsum_coe_eq (NNReal.hasSum_real_toNNReal_of_nonneg hf_nonneg hf)]

section

variable [EMetricSpace Î²]

open ENNReal Filter EMetric

/-- In an emetric ball, the distance between points is everywhere finite -/
theorem edist_ne_top_of_mem_ball {a : Î²} {r : â„â‰¥0âˆ} (x y : ball a r) : edist x.1 y.1 â‰  âˆ :=
  ne_of_lt <|
    calc
      edist x y â‰¤ edist a x + edist a y := edist_triangle_left x.1 y.1 a
      _ < r + r := by rw [edist_comm a x, edist_comm a y]; exact ENNReal.add_lt_add x.2 y.2
      _ â‰¤ âˆ := le_top

/-- Each ball in an extended metric space gives us a metric space, as the edist
is everywhere finite. -/
def metricSpaceEMetricBall (a : Î²) (r : â„â‰¥0âˆ) : MetricSpace (ball a r) :=
  EMetricSpace.toMetricSpace edist_ne_top_of_mem_ball

theorem nhds_eq_nhds_emetric_ball (a x : Î²) (r : â„â‰¥0âˆ) (h : x âˆˆ ball a r) :
    ğ“ x = map ((â†‘) : ball a r â†’ Î²) (ğ“ âŸ¨x, hâŸ©) :=
  (map_nhds_subtype_coe_eq_nhds _ <| IsOpen.mem_nhds EMetric.isOpen_ball h).symm

end

section

variable [PseudoEMetricSpace Î±]

open EMetric

theorem tendsto_iff_edist_tendsto_0 {l : Filter Î²} {f : Î² â†’ Î±} {y : Î±} :
    Tendsto f l (ğ“ y) â†” Tendsto (fun x => edist (f x) y) l (ğ“ 0) := by
  simp only [EMetric.nhds_basis_eball.tendsto_right_iff, EMetric.mem_ball,
    @tendsto_order â„â‰¥0âˆ Î² _ _, forall_prop_of_false ENNReal.not_lt_zero, forall_const, true_and]

/-- Yet another metric characterization of Cauchy sequences on integers. This one is often the
most efficient. -/
theorem EMetric.cauchySeq_iff_le_tendsto_0 [Nonempty Î²] [SemilatticeSup Î²] {s : Î² â†’ Î±} :
    CauchySeq s â†” âˆƒ b : Î² â†’ â„â‰¥0âˆ, (âˆ€ n m N : Î², N â‰¤ n â†’ N â‰¤ m â†’ edist (s n) (s m) â‰¤ b N) âˆ§
      Tendsto b atTop (ğ“ 0) := EMetric.cauchySeq_iff.trans <| by
  constructor
  Â· intro hs
    /- `s` is Cauchy sequence. Let `b n` be the diameter of the set `s '' Set.Ici n`. -/
    refine âŸ¨fun N => EMetric.diam (s '' Ici N), fun n m N hn hm => ?_, ?_âŸ©
    -- Prove that it bounds the distances of points in the Cauchy sequence
    Â· exact EMetric.edist_le_diam_of_mem (mem_image_of_mem _ hn) (mem_image_of_mem _ hm)
    -- Prove that it tends to `0`, by using the Cauchy property of `s`
    Â· refine ENNReal.tendsto_nhds_zero.2 fun Îµ Îµ0 => ?_
      rcases hs Îµ Îµ0 with âŸ¨N, hNâŸ©
      refine (eventually_ge_atTop N).mono fun n hn => EMetric.diam_le ?_
      rintro _ âŸ¨k, hk, rflâŸ© _ âŸ¨l, hl, rflâŸ©
      exact (hN _ (hn.trans hk) _ (hn.trans hl)).le
  Â· rintro âŸ¨b, âŸ¨b_bound, b_limâŸ©âŸ© Îµ Îµpos
    have : âˆ€á¶  n in atTop, b n < Îµ := b_lim.eventually (gt_mem_nhds Îµpos)
    rcases this.exists with âŸ¨N, hNâŸ©
    refine âŸ¨N, fun m hm n hn => ?_âŸ©
    calc edist (s m) (s n) â‰¤ b N := b_bound m n N hm hn
    _ < Îµ := hN

theorem continuous_of_le_add_edist {f : Î± â†’ â„â‰¥0âˆ} (C : â„â‰¥0âˆ) (hC : C â‰  âˆ)
    (h : âˆ€ x y, f x â‰¤ f y + C * edist x y) : Continuous f := by
  refine continuous_iff_continuousAt.2 fun x => ENNReal.tendsto_nhds_of_Icc fun Îµ Îµ0 => ?_
  rcases ENNReal.exists_nnreal_pos_mul_lt hC Îµ0.ne' with âŸ¨Î´, Î´0, hÎ´âŸ©
  rw [mul_comm] at hÎ´
  filter_upwards [EMetric.closedBall_mem_nhds x (ENNReal.coe_pos.2 Î´0)] with y hy
  refine âŸ¨tsub_le_iff_right.2 <| (h x y).trans ?_, (h y x).trans ?_âŸ© <;>
    refine add_le_add_left (le_trans (mul_le_mul_left' ?_ _) hÎ´.le) _
  exacts [EMetric.mem_closedBall'.1 hy, EMetric.mem_closedBall.1 hy]

theorem continuous_edist : Continuous fun p : Î± Ã— Î± => edist p.1 p.2 := by
  apply continuous_of_le_add_edist 2 (by decide)
  rintro âŸ¨x, yâŸ© âŸ¨x', y'âŸ©
  calc
    edist x y â‰¤ edist x x' + edist x' y' + edist y' y := edist_triangle4 _ _ _ _
    _ = edist x' y' + (edist x x' + edist y y') := by simp only [edist_comm]; ac_rfl
    _ â‰¤ edist x' y' + (edist (x, y) (x', y') + edist (x, y) (x', y')) := by
      gcongr <;> apply_rules [le_max_left, le_max_right]
    _ = edist x' y' + 2 * edist (x, y) (x', y') := by rw [â† mul_two, mul_comm]

@[continuity, fun_prop]
theorem Continuous.edist [TopologicalSpace Î²] {f g : Î² â†’ Î±} (hf : Continuous f)
    (hg : Continuous g) : Continuous fun b => edist (f b) (g b) :=
  continuous_edist.comp (hf.prodMk hg :)

theorem Filter.Tendsto.edist {f g : Î² â†’ Î±} {x : Filter Î²} {a b : Î±} (hf : Tendsto f x (ğ“ a))
    (hg : Tendsto g x (ğ“ b)) : Tendsto (fun x => edist (f x) (g x)) x (ğ“ (edist a b)) :=
  (continuous_edist.tendsto (a, b)).comp (hf.prodMk_nhds hg)

/-- If the extended distance between consecutive points of a sequence is estimated
by a summable series of `NNReal`s, then the original sequence is a Cauchy sequence. -/
theorem cauchySeq_of_edist_le_of_summable {f : â„• â†’ Î±} (d : â„• â†’ â„â‰¥0)
    (hf : âˆ€ n, edist (f n) (f n.succ) â‰¤ d n) (hd : Summable d) : CauchySeq f := by
  refine EMetric.cauchySeq_iff_NNReal.2 fun Îµ Îµpos â†¦ ?_
  -- Actually we need partial sums of `d` to be a Cauchy sequence.
  replace hd : CauchySeq fun n : â„• â†¦ âˆ‘ x âˆˆ Finset.range n, d x :=
    let âŸ¨_, HâŸ© := hd
    H.tendsto_sum_nat.cauchySeq
  -- Now we take the same `N` as in one of the definitions of a Cauchy sequence.
  refine (Metric.cauchySeq_iff'.1 hd Îµ (NNReal.coe_pos.2 Îµpos)).imp fun N hN n hn â†¦ ?_
  specialize hN n hn
  -- We simplify the known inequality.
  rw [dist_nndist, NNReal.nndist_eq, â† Finset.sum_range_add_sum_Ico _ hn, add_tsub_cancel_left,
    NNReal.coe_lt_coe, max_lt_iff] at hN
  rw [edist_comm]
  -- Then use `hf` to simplify the goal to the same form.
  refine lt_of_le_of_lt (edist_le_Ico_sum_of_edist_le hn fun _ _ â†¦ hf _) ?_
  exact mod_cast hN.1

theorem cauchySeq_of_edist_le_of_tsum_ne_top {f : â„• â†’ Î±} (d : â„• â†’ â„â‰¥0âˆ)
    (hf : âˆ€ n, edist (f n) (f n.succ) â‰¤ d n) (hd : tsum d â‰  âˆ) : CauchySeq f := by
  lift d to â„• â†’ NNReal using fun i => ENNReal.ne_top_of_tsum_ne_top hd i
  rw [ENNReal.tsum_coe_ne_top_iff_summable] at hd
  exact cauchySeq_of_edist_le_of_summable d hf hd

theorem EMetric.isClosed_closedBall {a : Î±} {r : â„â‰¥0âˆ} : IsClosed (closedBall a r) :=
  isClosed_le (continuous_id.edist continuous_const) continuous_const

@[simp]
theorem EMetric.diam_closure (s : Set Î±) : diam (closure s) = diam s := by
  refine le_antisymm (diam_le fun x hx y hy => ?_) (diam_mono subset_closure)
  have : edist x y âˆˆ closure (Iic (diam s)) :=
    map_mem_closureâ‚‚ continuous_edist hx hy fun x hx y hy => edist_le_diam_of_mem hx hy
  rwa [closure_Iic] at this

@[simp]
theorem Metric.diam_closure {Î± : Type*} [PseudoMetricSpace Î±] (s : Set Î±) :
    Metric.diam (closure s) = diam s := by simp only [Metric.diam, EMetric.diam_closure]

theorem isClosed_setOf_lipschitzOnWith {Î± Î²} [PseudoEMetricSpace Î±] [PseudoEMetricSpace Î²] (K : â„â‰¥0)
    (s : Set Î±) : IsClosed { f : Î± â†’ Î² | LipschitzOnWith K f s } := by
  simp only [LipschitzOnWith, setOf_forall]
  refine isClosed_biInter fun x _ => isClosed_biInter fun y _ => isClosed_le ?_ ?_
  exacts [.edist (continuous_apply x) (continuous_apply y), continuous_const]

theorem isClosed_setOf_lipschitzWith {Î± Î²} [PseudoEMetricSpace Î±] [PseudoEMetricSpace Î²] (K : â„â‰¥0) :
    IsClosed { f : Î± â†’ Î² | LipschitzWith K f } := by
  simp only [â† lipschitzOnWith_univ, isClosed_setOf_lipschitzOnWith]

namespace Real

/-- For a bounded set `s : Set â„`, its `EMetric.diam` is equal to `sSup s - sInf s` reinterpreted as
`â„â‰¥0âˆ`. -/
theorem ediam_eq {s : Set â„} (h : Bornology.IsBounded s) :
    EMetric.diam s = ENNReal.ofReal (sSup s - sInf s) := by
  rcases eq_empty_or_nonempty s with (rfl | hne)
  Â· simp
  refine le_antisymm (Metric.ediam_le_of_forall_dist_le fun x hx y hy => ?_) ?_
  Â· exact Real.dist_le_of_mem_Icc (h.subset_Icc_sInf_sSup hx) (h.subset_Icc_sInf_sSup hy)
  Â· apply ENNReal.ofReal_le_of_le_toReal
    rw [â† Metric.diam, â† Metric.diam_closure]
    calc sSup s - sInf s â‰¤ dist (sSup s) (sInf s) := le_abs_self _
    _ â‰¤ Metric.diam (closure s) := dist_le_diam_of_mem h.closure (csSup_mem_closure hne h.bddAbove)
        (csInf_mem_closure hne h.bddBelow)

/-- For a bounded set `s : Set â„`, its `Metric.diam` is equal to `sSup s - sInf s`. -/
theorem diam_eq {s : Set â„} (h : Bornology.IsBounded s) : Metric.diam s = sSup s - sInf s := by
  rw [Metric.diam, Real.ediam_eq h, ENNReal.toReal_ofReal]
  exact sub_nonneg.2 (Real.sInf_le_sSup s h.bddBelow h.bddAbove)

@[simp]
theorem ediam_Ioo (a b : â„) : EMetric.diam (Ioo a b) = ENNReal.ofReal (b - a) := by
  rcases le_or_gt b a with (h | h)
  Â· simp [h]
  Â· rw [Real.ediam_eq (isBounded_Ioo _ _), csSup_Ioo h, csInf_Ioo h]

@[simp]
theorem ediam_Icc (a b : â„) : EMetric.diam (Icc a b) = ENNReal.ofReal (b - a) := by
  rcases le_or_gt a b with (h | h)
  Â· rw [Real.ediam_eq (isBounded_Icc _ _), csSup_Icc h, csInf_Icc h]
  Â· simp [h, h.le]

@[simp]
theorem ediam_Ico (a b : â„) : EMetric.diam (Ico a b) = ENNReal.ofReal (b - a) :=
  le_antisymm (ediam_Icc a b â–¸ diam_mono Ico_subset_Icc_self)
    (ediam_Ioo a b â–¸ diam_mono Ioo_subset_Ico_self)

@[simp]
theorem ediam_Ioc (a b : â„) : EMetric.diam (Ioc a b) = ENNReal.ofReal (b - a) :=
  le_antisymm (ediam_Icc a b â–¸ diam_mono Ioc_subset_Icc_self)
    (ediam_Ioo a b â–¸ diam_mono Ioo_subset_Ioc_self)

theorem diam_Icc {a b : â„} (h : a â‰¤ b) : Metric.diam (Icc a b) = b - a := by
  simp [Metric.diam, ENNReal.toReal_ofReal (sub_nonneg.2 h)]

theorem diam_Ico {a b : â„} (h : a â‰¤ b) : Metric.diam (Ico a b) = b - a := by
  simp [Metric.diam, ENNReal.toReal_ofReal (sub_nonneg.2 h)]

theorem diam_Ioc {a b : â„} (h : a â‰¤ b) : Metric.diam (Ioc a b) = b - a := by
  simp [Metric.diam, ENNReal.toReal_ofReal (sub_nonneg.2 h)]

theorem diam_Ioo {a b : â„} (h : a â‰¤ b) : Metric.diam (Ioo a b) = b - a := by
  simp [Metric.diam, ENNReal.toReal_ofReal (sub_nonneg.2 h)]

end Real

/-- If `edist (f n) (f (n+1))` is bounded above by a function `d : â„• â†’ â„â‰¥0âˆ`,
then the distance from `f n` to the limit is bounded by `âˆ‘'_{k=n}^âˆ d k`. -/
theorem edist_le_tsum_of_edist_le_of_tendsto {f : â„• â†’ Î±} (d : â„• â†’ â„â‰¥0âˆ)
    (hf : âˆ€ n, edist (f n) (f n.succ) â‰¤ d n) {a : Î±} (ha : Tendsto f atTop (ğ“ a)) (n : â„•) :
    edist (f n) a â‰¤ âˆ‘' m, d (n + m) := by
  refine le_of_tendsto (tendsto_const_nhds.edist ha) (mem_atTop_sets.2 âŸ¨n, fun m hnm => ?_âŸ©)
  change edist _ _ â‰¤ _
  refine le_trans (edist_le_Ico_sum_of_edist_le hnm fun _ _ => hf _) ?_
  rw [Finset.sum_Ico_eq_sum_range]
  exact ENNReal.summable.sum_le_tsum _ (fun _ _ => zero_le _)

/-- If `edist (f n) (f (n+1))` is bounded above by a function `d : â„• â†’ â„â‰¥0âˆ`,
then the distance from `f 0` to the limit is bounded by `âˆ‘'_{k=0}^âˆ d k`. -/
theorem edist_le_tsum_of_edist_le_of_tendstoâ‚€ {f : â„• â†’ Î±} (d : â„• â†’ â„â‰¥0âˆ)
    (hf : âˆ€ n, edist (f n) (f n.succ) â‰¤ d n) {a : Î±} (ha : Tendsto f atTop (ğ“ a)) :
    edist (f 0) a â‰¤ âˆ‘' m, d m := by simpa using edist_le_tsum_of_edist_le_of_tendsto d hf ha 0

end

namespace ENNReal

section truncateToReal

/-- With truncation level `t`, the truncated cast `â„â‰¥0âˆ â†’ â„` is given by `x â†¦ (min t x).toReal`.
Unlike `ENNReal.toReal`, this cast is continuous and monotone when `t â‰  âˆ`. -/
noncomputable def truncateToReal (t x : â„â‰¥0âˆ) : â„ := (min t x).toReal

lemma truncateToReal_eq_toReal {t x : â„â‰¥0âˆ} (t_ne_top : t â‰  âˆ) (x_le : x â‰¤ t) :
    truncateToReal t x = x.toReal := by
  have x_lt_top : x < âˆ := lt_of_le_of_lt x_le t_ne_top.lt_top
  have obs : min t x â‰  âˆ := by
    simp_all only [ne_eq, min_eq_top, false_and, not_false_eq_true]
  exact (ENNReal.toReal_eq_toReal obs x_lt_top.ne).mpr (min_eq_right x_le)

lemma truncateToReal_le {t : â„â‰¥0âˆ} (t_ne_top : t â‰  âˆ) {x : â„â‰¥0âˆ} :
    truncateToReal t x â‰¤ t.toReal := by
  rw [truncateToReal]
  gcongr
  exacts [t_ne_top, min_le_left t x]

lemma truncateToReal_nonneg {t x : â„â‰¥0âˆ} : 0 â‰¤ truncateToReal t x := toReal_nonneg

/-- The truncated cast `ENNReal.truncateToReal t : â„â‰¥0âˆ â†’ â„` is monotone when `t â‰  âˆ`. -/
lemma monotone_truncateToReal {t : â„â‰¥0âˆ} (t_ne_top : t â‰  âˆ) : Monotone (truncateToReal t) := by
  intro x y x_le_y
  simp only [truncateToReal]
  gcongr
  exact ne_top_of_le_ne_top t_ne_top (min_le_left _ _)

/-- The truncated cast `ENNReal.truncateToReal t : â„â‰¥0âˆ â†’ â„` is continuous when `t â‰  âˆ`. -/
@[fun_prop]
lemma continuous_truncateToReal {t : â„â‰¥0âˆ} (t_ne_top : t â‰  âˆ) : Continuous (truncateToReal t) := by
  apply continuousOn_toReal.comp_continuous (by fun_prop)
  simp [t_ne_top]

end truncateToReal

section LimsupLiminf

variable {Î¹ : Type*} {f : Filter Î¹} {u v : Î¹ â†’ â„â‰¥0âˆ}

lemma limsup_sub_const (F : Filter Î¹) (f : Î¹ â†’ â„â‰¥0âˆ) (c : â„â‰¥0âˆ) :
    Filter.limsup (fun i â†¦ f i - c) F = Filter.limsup f F - c := by
  rcases F.eq_or_neBot with rfl | _
  Â· simp only [limsup_bot, bot_eq_zero', zero_le, tsub_eq_zero_of_le]
  Â· exact (Monotone.map_limsSup_of_continuousAt (F := F.map f) (f := fun (x : â„â‰¥0âˆ) â†¦ x - c)
    (fun _ _ h â†¦ tsub_le_tsub_right h c) (continuous_sub_right c).continuousAt).symm

lemma liminf_sub_const (F : Filter Î¹) [NeBot F] (f : Î¹ â†’ â„â‰¥0âˆ) (c : â„â‰¥0âˆ) :
    Filter.liminf (fun i â†¦ f i - c) F = Filter.liminf f F - c :=
  (Monotone.map_limsInf_of_continuousAt (F := F.map f) (f := fun (x : â„â‰¥0âˆ) â†¦ x - c)
    (fun _ _ h â†¦ tsub_le_tsub_right h c) (continuous_sub_right c).continuousAt).symm

lemma limsup_const_sub (F : Filter Î¹) (f : Î¹ â†’ â„â‰¥0âˆ) {c : â„â‰¥0âˆ} (c_ne_top : c â‰  âˆ) :
    Filter.limsup (fun i â†¦ c - f i) F = c - Filter.liminf f F := by
  rcases F.eq_or_neBot with rfl | _
  Â· simp only [limsup_bot, bot_eq_zero', liminf_bot, le_top, tsub_eq_zero_of_le]
  Â· exact (Antitone.map_limsInf_of_continuousAt (F := F.map f) (f := fun (x : â„â‰¥0âˆ) â†¦ c - x)
    (fun _ _ h â†¦ tsub_le_tsub_left h c) (continuous_sub_left c_ne_top).continuousAt).symm

lemma liminf_const_sub (F : Filter Î¹) [NeBot F] (f : Î¹ â†’ â„â‰¥0âˆ) {c : â„â‰¥0âˆ} (c_ne_top : c â‰  âˆ) :
    Filter.liminf (fun i â†¦ c - f i) F = c - Filter.limsup f F :=
  (Antitone.map_limsSup_of_continuousAt (F := F.map f) (f := fun (x : â„â‰¥0âˆ) â†¦ c - x)
    (fun _ _ h â†¦ tsub_le_tsub_left h c) (continuous_sub_left c_ne_top).continuousAt).symm

lemma le_limsup_mul : limsup u f * liminf v f â‰¤ limsup (u * v) f :=
  mul_le_of_forall_lt fun a a_u b b_v â†¦ (le_limsup_iff).2 fun c c_ab â†¦
    Frequently.mono (Frequently.and_eventually ((frequently_lt_of_lt_limsup) a_u)
    ((eventually_lt_of_lt_liminf) b_v)) fun _ ab_x â†¦ c_ab.trans (mul_lt_mul ab_x.1 ab_x.2)

/-- See also `ENNReal.limsup_mul_le`. -/
lemma limsup_mul_le' (h : limsup u f â‰  0 âˆ¨ limsup v f â‰  âˆ) (h' : limsup u f â‰  âˆ âˆ¨ limsup v f â‰  0) :
    limsup (u * v) f â‰¤ limsup u f * limsup v f := by
  refine le_mul_of_forall_lt h h' fun a a_u b b_v â†¦ (limsup_le_iff).2 fun c c_ab â†¦ ?_
  filter_upwards [eventually_lt_of_limsup_lt a_u, eventually_lt_of_limsup_lt b_v] with x a_x b_x
  exact (mul_lt_mul a_x b_x).trans c_ab

lemma le_liminf_mul : liminf u f * liminf v f â‰¤ liminf (u * v) f := by
  refine mul_le_of_forall_lt fun a a_u b b_v â†¦ (le_liminf_iff).2 fun c c_ab â†¦ ?_
  filter_upwards [eventually_lt_of_lt_liminf a_u, eventually_lt_of_lt_liminf b_v] with x a_x b_x
  exact c_ab.trans (mul_lt_mul a_x b_x)

lemma liminf_mul_le (h : limsup u f â‰  0 âˆ¨ liminf v f â‰  âˆ) (h' : limsup u f â‰  âˆ âˆ¨ liminf v f â‰  0) :
    liminf (u * v) f â‰¤ limsup u f * liminf v f :=
  le_mul_of_forall_lt h h' fun a a_u b b_v â†¦ (liminf_le_iff).2 fun c c_ab â†¦
    Frequently.mono (((frequently_lt_of_liminf_lt) b_v).and_eventually
    ((eventually_lt_of_limsup_lt) a_u)) fun _ ab_x â†¦ (mul_lt_mul ab_x.2 ab_x.1).trans c_ab

/-- If `u : Î¹ â†’ â„â‰¥0âˆ` is bounded, then we have `liminf (toReal âˆ˜ u) = toReal (liminf u)`. -/
lemma liminf_toReal_eq [NeBot f] {b : â„â‰¥0âˆ} (b_ne_top : b â‰  âˆ) (le_b : âˆ€á¶  i in f, u i â‰¤ b) :
    f.liminf (fun i â†¦ (u i).toReal) = (f.liminf u).toReal := by
  have liminf_le : f.liminf u â‰¤ b := by
    apply liminf_le_of_le âŸ¨0, by simpâŸ©
    intro y h
    obtain âŸ¨i, hiâŸ© := (Eventually.and h le_b).exists
    exact hi.1.trans hi.2
  have aux : âˆ€á¶  i in f, (u i).toReal = ENNReal.truncateToReal b (u i) := by
    filter_upwards [le_b] with i i_le_b
    simp only [truncateToReal_eq_toReal b_ne_top i_le_b]
  have aux' : (f.liminf u).toReal = ENNReal.truncateToReal b (f.liminf u) := by
    rw [truncateToReal_eq_toReal b_ne_top liminf_le]
  simp_rw [liminf_congr aux, aux']
  have key := Monotone.map_liminf_of_continuousAt (F := f) (monotone_truncateToReal b_ne_top) u
          (continuous_truncateToReal b_ne_top).continuousAt
          (IsBoundedUnder.isCoboundedUnder_ge âŸ¨b, by simpa only [eventually_map] using le_bâŸ©)
          âŸ¨0, Eventually.of_forall (by simp)âŸ©
  rw [key]
  rfl

/-- If `u : Î¹ â†’ â„â‰¥0âˆ` is bounded, then we have `liminf (toReal âˆ˜ u) = toReal (liminf u)`. -/
lemma limsup_toReal_eq [NeBot f] {b : â„â‰¥0âˆ} (b_ne_top : b â‰  âˆ) (le_b : âˆ€á¶  i in f, u i â‰¤ b) :
    f.limsup (fun i â†¦ (u i).toReal) = (f.limsup u).toReal := by
  have aux : âˆ€á¶  i in f, (u i).toReal = ENNReal.truncateToReal b (u i) := by
    filter_upwards [le_b] with i i_le_b
    simp only [truncateToReal_eq_toReal b_ne_top i_le_b]
  have aux' : (f.limsup u).toReal = ENNReal.truncateToReal b (f.limsup u) := by
    rw [truncateToReal_eq_toReal b_ne_top (limsup_le_of_le âŸ¨0, by simpâŸ© le_b)]
  simp_rw [limsup_congr aux, aux']
  have key := Monotone.map_limsup_of_continuousAt (F := f) (monotone_truncateToReal b_ne_top) u
          (continuous_truncateToReal b_ne_top).continuousAt
          âŸ¨b, by simpa only [eventually_map] using le_bâŸ©
          (IsBoundedUnder.isCoboundedUnder_le âŸ¨0, Eventually.of_forall (by simp)âŸ©)
  rw [key]
  rfl

@[simp, norm_cast]
lemma ofNNReal_limsup {u : Î¹ â†’ â„â‰¥0} (hf : f.IsBoundedUnder (Â· â‰¤ Â·) u) :
    limsup u f = limsup (fun i â†¦ (u i : â„â‰¥0âˆ)) f := by
  refine eq_of_forall_nnreal_iff fun r â†¦ ?_
  rw [coe_le_coe, le_limsup_iff, le_limsup_iff]
  simp [forall_ennreal]

@[simp, norm_cast]
lemma ofNNReal_liminf {u : Î¹ â†’ â„â‰¥0} (hf : f.IsCoboundedUnder (Â· â‰¥ Â·) u) :
    liminf u f = liminf (fun i â†¦ (u i : â„â‰¥0âˆ)) f := by
  refine eq_of_forall_nnreal_iff fun r â†¦ ?_
  rw [coe_le_coe, le_liminf_iff, le_liminf_iff]
  simp [forall_ennreal]

theorem liminf_add_of_right_tendsto_zero {u : Filter Î¹} {g : Î¹ â†’ â„â‰¥0âˆ} (hg : u.Tendsto g (ğ“ 0))
    (f : Î¹ â†’ â„â‰¥0âˆ) : u.liminf (f + g) = u.liminf f := by
  refine le_antisymm ?_ <| liminf_le_liminf <| .of_forall <| by simp
  refine liminf_le_of_le (by isBoundedDefault) fun b hb â†¦ ?_
  rw [Filter.le_liminf_iff']
  rintro a hab
  filter_upwards [hb, ENNReal.tendsto_nhds_zero.1 hg _ <| lt_min (tsub_pos_of_lt hab) one_pos]
    with i hfg hg
  exact ENNReal.le_of_add_le_add_right (hg.trans_lt <| by simp).ne <|
    (add_le_of_le_tsub_left_of_le hab.le <| hg.trans <| min_le_left ..).trans hfg

theorem liminf_add_of_left_tendsto_zero {u : Filter Î¹} {f : Î¹ â†’ â„â‰¥0âˆ} (hf : u.Tendsto f (ğ“ 0))
    (g : Î¹ â†’ â„â‰¥0âˆ) : u.liminf (f + g) = u.liminf g := by
  rw [add_comm, liminf_add_of_right_tendsto_zero hf]

theorem limsup_add_of_right_tendsto_zero {u : Filter Î¹} {g : Î¹ â†’ â„â‰¥0âˆ} (hg : u.Tendsto g (ğ“ 0))
    (f : Î¹ â†’ â„â‰¥0âˆ) : u.limsup (f + g) = u.limsup f := by
  refine le_antisymm ?_ <| limsup_le_limsup <| .of_forall <| by simp
  refine le_limsup_of_le (by isBoundedDefault) fun b hb â†¦ ?_
  rw [Filter.limsup_le_iff']
  rintro a hba
  filter_upwards [hb, ENNReal.tendsto_nhds_zero.1 hg _ <| tsub_pos_of_lt hba] with i hf hg
  calc  f i + g i
    _ â‰¤ b + g i := by gcongr
    _ â‰¤ a := add_le_of_le_tsub_left_of_le hba.le hg

theorem limsup_add_of_left_tendsto_zero {u : Filter Î¹} {f : Î¹ â†’ â„â‰¥0âˆ} (hf : u.Tendsto f (ğ“ 0))
    (g : Î¹ â†’ â„â‰¥0âˆ) : u.limsup (f + g) = u.limsup g := by
  rw [add_comm, limsup_add_of_right_tendsto_zero hf]

end LimsupLiminf

end ENNReal -- namespace
