/-
Copyright (c) 2021 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Data.Rat.Encodable
import Mathlib.Data.Real.EReal
import Mathlib.Topology.Instances.ENNReal
import Mathlib.Topology.Order.MonotoneContinuity

#align_import topology.instances.ereal from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Topological structure on `EReal`

We endow `EReal` with the order topology, and prove basic properties of this topology.

## Main results

* `Real.toEReal : â„ â†’ EReal` is an open embedding
* `ENNReal.toEReal : â„â‰¥0âˆ â†’ EReal` is a closed embedding
* The addition on `EReal` is continuous except at `(âŠ¥, âŠ¤)` and at `(âŠ¤, âŠ¥)`.
* Negation is a homeomorphism on `EReal`.

## Implementation

Most proofs are adapted from the corresponding proofs on `â„â‰¥0âˆ`.
-/

noncomputable section

open scoped Classical
open Set Filter Metric TopologicalSpace Topology
open scoped ENNReal NNReal Filter

variable {Î± : Type*} [TopologicalSpace Î±]

namespace EReal

instance : TopologicalSpace EReal := Preorder.topology EReal
instance : OrderTopology EReal := âŸ¨rflâŸ©
instance : T5Space EReal := inferInstance
instance : T2Space EReal := inferInstance

lemma denseRange_ratCast : DenseRange (fun r : â„š â†¦ ((r : â„) : EReal)) :=
  dense_of_exists_between fun _ _ h => exists_range_iff.2 <| exists_rat_btwn_of_lt h

instance : SecondCountableTopology EReal :=
  have : SeparableSpace EReal := âŸ¨âŸ¨_, countable_range _, denseRange_ratCastâŸ©âŸ©
  .of_separableSpace_orderTopology _

/-! ### Real coercion -/

theorem embedding_coe : Embedding ((â†‘) : â„ â†’ EReal) :=
  coe_strictMono.embedding_of_ordConnected <| by rw [range_coe_eq_Ioo]; exact ordConnected_Ioo
#align ereal.embedding_coe EReal.embedding_coe

theorem openEmbedding_coe : OpenEmbedding ((â†‘) : â„ â†’ EReal) :=
  âŸ¨embedding_coe, by simp only [range_coe_eq_Ioo, isOpen_Ioo]âŸ©
#align ereal.open_embedding_coe EReal.openEmbedding_coe

@[norm_cast]
theorem tendsto_coe {Î± : Type*} {f : Filter Î±} {m : Î± â†’ â„} {a : â„} :
    Tendsto (fun a => (m a : EReal)) f (ğ“ â†‘a) â†” Tendsto m f (ğ“ a) :=
  embedding_coe.tendsto_nhds_iff.symm
#align ereal.tendsto_coe EReal.tendsto_coe

theorem _root_.continuous_coe_real_ereal : Continuous ((â†‘) : â„ â†’ EReal) :=
  embedding_coe.continuous
#align continuous_coe_real_ereal continuous_coe_real_ereal

theorem continuous_coe_iff {f : Î± â†’ â„} : (Continuous fun a => (f a : EReal)) â†” Continuous f :=
  embedding_coe.continuous_iff.symm
#align ereal.continuous_coe_iff EReal.continuous_coe_iff

theorem nhds_coe {r : â„} : ğ“ (r : EReal) = (ğ“ r).map (â†‘) :=
  (openEmbedding_coe.map_nhds_eq r).symm
#align ereal.nhds_coe EReal.nhds_coe

theorem nhds_coe_coe {r p : â„} :
    ğ“ ((r : EReal), (p : EReal)) = (ğ“ (r, p)).map fun p : â„ Ã— â„ => (â†‘p.1, â†‘p.2) :=
  ((openEmbedding_coe.prod openEmbedding_coe).map_nhds_eq (r, p)).symm
#align ereal.nhds_coe_coe EReal.nhds_coe_coe

theorem tendsto_toReal {a : EReal} (ha : a â‰  âŠ¤) (h'a : a â‰  âŠ¥) :
    Tendsto EReal.toReal (ğ“ a) (ğ“ a.toReal) := by
  lift a to â„ using âŸ¨ha, h'aâŸ©
  rw [nhds_coe, tendsto_map'_iff]
  exact tendsto_id
#align ereal.tendsto_to_real EReal.tendsto_toReal

theorem continuousOn_toReal : ContinuousOn EReal.toReal ({âŠ¥, âŠ¤}á¶œ : Set EReal) := fun _a ha =>
  ContinuousAt.continuousWithinAt (tendsto_toReal (mt Or.inr ha) (mt Or.inl ha))
#align ereal.continuous_on_to_real EReal.continuousOn_toReal

/-- The set of finite `EReal` numbers is homeomorphic to `â„`. -/
def neBotTopHomeomorphReal : ({âŠ¥, âŠ¤}á¶œ : Set EReal) â‰ƒâ‚œ â„ where
  toEquiv := neTopBotEquivReal
  continuous_toFun := continuousOn_iff_continuous_restrict.1 continuousOn_toReal
  continuous_invFun := continuous_coe_real_ereal.subtype_mk _
#align ereal.ne_bot_top_homeomorph_real EReal.neBotTopHomeomorphReal

/-! ### ennreal coercion -/

theorem embedding_coe_ennreal : Embedding ((â†‘) : â„â‰¥0âˆ â†’ EReal) :=
  coe_ennreal_strictMono.embedding_of_ordConnected <| by
    rw [range_coe_ennreal]; exact ordConnected_Ici
#align ereal.embedding_coe_ennreal EReal.embedding_coe_ennreal

theorem closedEmbedding_coe_ennreal : ClosedEmbedding ((â†‘) : â„â‰¥0âˆ â†’ EReal) :=
  âŸ¨embedding_coe_ennreal, by rw [range_coe_ennreal]; exact isClosed_IciâŸ©

@[norm_cast]
theorem tendsto_coe_ennreal {Î± : Type*} {f : Filter Î±} {m : Î± â†’ â„â‰¥0âˆ} {a : â„â‰¥0âˆ} :
    Tendsto (fun a => (m a : EReal)) f (ğ“ â†‘a) â†” Tendsto m f (ğ“ a) :=
  embedding_coe_ennreal.tendsto_nhds_iff.symm
#align ereal.tendsto_coe_ennreal EReal.tendsto_coe_ennreal

theorem _root_.continuous_coe_ennreal_ereal : Continuous ((â†‘) : â„â‰¥0âˆ â†’ EReal) :=
  embedding_coe_ennreal.continuous
#align continuous_coe_ennreal_ereal continuous_coe_ennreal_ereal

theorem continuous_coe_ennreal_iff {f : Î± â†’ â„â‰¥0âˆ} :
    (Continuous fun a => (f a : EReal)) â†” Continuous f :=
  embedding_coe_ennreal.continuous_iff.symm
#align ereal.continuous_coe_ennreal_iff EReal.continuous_coe_ennreal_iff

/-! ### Neighborhoods of infinity -/

theorem nhds_top : ğ“ (âŠ¤ : EReal) = â¨… (a) (_ : a â‰  âŠ¤), ğ“Ÿ (Ioi a) :=
  nhds_top_order.trans <| by simp only [lt_top_iff_ne_top]
#align ereal.nhds_top EReal.nhds_top

nonrec theorem nhds_top_basis : (ğ“ (âŠ¤ : EReal)).HasBasis (fun _ : â„ â†¦ True) (Ioi Â·) := by
  refine nhds_top_basis.to_hasBasis (fun x hx => ?_) fun _ _ â†¦ âŸ¨_, coe_lt_top _, Subset.rflâŸ©
  rcases exists_rat_btwn_of_lt hx with âŸ¨y, hxy, -âŸ©
  exact âŸ¨_, trivial, Ioi_subset_Ioi hxy.leâŸ©

theorem nhds_top' : ğ“ (âŠ¤ : EReal) = â¨… a : â„, ğ“Ÿ (Ioi â†‘a) := nhds_top_basis.eq_iInf
#align ereal.nhds_top' EReal.nhds_top'

theorem mem_nhds_top_iff {s : Set EReal} : s âˆˆ ğ“ (âŠ¤ : EReal) â†” âˆƒ y : â„, Ioi (y : EReal) âŠ† s :=
  nhds_top_basis.mem_iff.trans <| by simp only [true_and]
#align ereal.mem_nhds_top_iff EReal.mem_nhds_top_iff

theorem tendsto_nhds_top_iff_real {Î± : Type*} {m : Î± â†’ EReal} {f : Filter Î±} :
    Tendsto m f (ğ“ âŠ¤) â†” âˆ€ x : â„, âˆ€á¶  a in f, â†‘x < m a :=
  nhds_top_basis.tendsto_right_iff.trans <| by simp only [true_implies, mem_Ioi]
#align ereal.tendsto_nhds_top_iff_real EReal.tendsto_nhds_top_iff_real

theorem nhds_bot : ğ“ (âŠ¥ : EReal) = â¨… (a) (_ : a â‰  âŠ¥), ğ“Ÿ (Iio a) :=
  nhds_bot_order.trans <| by simp only [bot_lt_iff_ne_bot]
#align ereal.nhds_bot EReal.nhds_bot

theorem nhds_bot_basis : (ğ“ (âŠ¥ : EReal)).HasBasis (fun _ : â„ â†¦ True) (Iio Â·) := by
  refine nhds_bot_basis.to_hasBasis (fun x hx => ?_) fun _ _ â†¦ âŸ¨_, bot_lt_coe _, Subset.rflâŸ©
  rcases exists_rat_btwn_of_lt hx with âŸ¨y, -, hxyâŸ©
  exact âŸ¨_, trivial, Iio_subset_Iio hxy.leâŸ©

theorem nhds_bot' : ğ“ (âŠ¥ : EReal) = â¨… a : â„, ğ“Ÿ (Iio â†‘a) :=
  nhds_bot_basis.eq_iInf
#align ereal.nhds_bot' EReal.nhds_bot'

theorem mem_nhds_bot_iff {s : Set EReal} : s âˆˆ ğ“ (âŠ¥ : EReal) â†” âˆƒ y : â„, Iio (y : EReal) âŠ† s :=
  nhds_bot_basis.mem_iff.trans <| by simp only [true_and]
#align ereal.mem_nhds_bot_iff EReal.mem_nhds_bot_iff

theorem tendsto_nhds_bot_iff_real {Î± : Type*} {m : Î± â†’ EReal} {f : Filter Î±} :
    Tendsto m f (ğ“ âŠ¥) â†” âˆ€ x : â„, âˆ€á¶  a in f, m a < x :=
  nhds_bot_basis.tendsto_right_iff.trans <| by simp only [true_implies, mem_Iio]
#align ereal.tendsto_nhds_bot_iff_real EReal.tendsto_nhds_bot_iff_real

/-! ### Liminfs and Limsups -/

/- The theorem `Filter.liminf_le_liminf` uses two hypotheses (that some sequences are bounded
under/above). These two hypotheses are always satisfied in EReal.
This specialization avoids them. -/
theorem liminf_le_liminf {Î± : Type*} {f : Filter Î±} {u v : Î± â†’ EReal} (h : u â‰¤á¶ [f] v) :
    liminf u f â‰¤ liminf v f := Filter.liminf_le_liminf h

/- The theorem `Filter.limsup_le_limsup` uses two hypotheses (that some sequences are bounded
under/above). These two hypotheses are always satisfied in EReal.
This specialization avoids them. -/
theorem limsup_le_limsup {Î± : Type*} {f : Filter Î±} {u v : Î± â†’ EReal} (h : u â‰¤á¶ [f] v) :
    limsup u f â‰¤ limsup v f := Filter.limsup_le_limsup h

theorem limsup_add_le_ltâ‚‚ {Î± : Type*} {f : Filter Î±} {u v : Î± â†’ EReal} {a b : EReal}
    (ha : limsup u f < a) (hb : limsup v f < b) : limsup (u + v) f â‰¤ a + b := by
  rcases eq_or_neBot f with (rfl | _); simp only [limsup_bot, bot_le]
  rw [â† @limsup_const EReal Î± _ f _ (a+b)]
  apply limsup_le_limsup (Eventually.mp (Eventually.and (eventually_lt_of_limsup_lt ha)
    (eventually_lt_of_limsup_lt hb)) (eventually_of_forall _))
  simp only [Pi.add_apply, and_imp]
  intro x
  exact fun ux_lt_a vx_lt_b â†¦ add_le_add (le_of_lt ux_lt_a) (le_of_lt vx_lt_b)

/-
TODO: Since `EReal.le_iff_le_forall_real_gt` is ported in `Mathlib.Data.Real.EReal`
via the PR `#14125`, we need to:
-- Wait for PR `#14125` to be merged
-- Rebase to master
-- Uncomment this theorem
-- Consider changing its name to `limsup_add_bot_of_ne_top` to stick to naming conventions

theorem limsup_add_bot_ne_top {Î± : Type _} {f : Filter Î±} {u : Î± â†’ EReal} {v : Î± â†’ EReal}
    (h : limsup u f = âŠ¥) (h' : limsup v f â‰  âŠ¤) :
    limsup (u+v) f = âŠ¥ := by
  apply le_bot_iff.1
  apply (EReal.le_iff_le_forall_real_gt âŠ¥ (limsup (u+v) f)).2
  intro x
  rcases EReal.exists_between_coe_real (Ne.lt_top h') with âŸ¨y, âŸ¨hy, _âŸ©âŸ©
  intro trash; clear trash
  rw [â† sub_add_cancel x y, EReal.coe_add (x-y) y, EReal.coe_sub x y]
  apply @EReal.limsup_add_le_ltâ‚‚ Î± f u v (x-y) y _ hy
  rw [h, â† EReal.coe_sub x y]
  exact EReal.bot_lt_coe (x-y)
-/

/-
TODO: Since `limsup_add_bot_ne_top` above is dependent from a lemma ported in
`Mathlib.Data.Real.EReal` via the PR #14125, we need to:
-- Wait for PR #14125 to be merged
-- Rebase to master
-- Uncomment this theorem

theorem limsup_add_le_add_limsup {Î± : Type _} {f : Filter Î±} {u v : Î± â†’ EReal}
    (h : limsup u f â‰  âŠ¥ âˆ¨ limsup v f â‰  âŠ¤) (h' : limsup u f â‰  âŠ¤ âˆ¨ limsup v f â‰  âŠ¥) :
    limsup (u + v) f â‰¤ (limsup u f) + (limsup v f) := by
  /- WLOG, âŠ¥ < limsup u f. -/
  rcases eq_bot_or_bot_lt (limsup u f) with (u_bot | u_nbot)
  Â· rcases h with (u_nbot | v_ntop)
    Â· exfalso; exact u_nbot u_bot
    Â· rw [EReal.limsup_add_bot_ne_top u_bot v_ntop]; exact bot_le
  /- WLOG, âŠ¥ < limsup v f. -/
  rcases eq_bot_or_bot_lt (limsup v f) with (v_bot | v_nbot)
  Â· rcases h' with (u_ntop | v_nbot)
    Â· rw [add_comm, EReal.limsup_add_bot_ne_top v_bot u_ntop]; exact bot_le
    Â· exfalso; exact v_nbot v_bot
  clear h h'
  /- WLOG, limsup v f < âŠ¤. -/
  rcases eq_top_or_lt_top (limsup v f) with (v_top | v_ntop)
  Â· rw [v_top, EReal.ne_bot_add_top (ne_of_gt u_nbot)]; exact le_top
  /- General case. -/
  have limsup_v_real := EReal.coe_toReal (ne_of_lt v_ntop) (ne_of_gt v_nbot)
  apply (EReal.le_iff_le_forall_real_gt _ _).2
  intros x hx
  rcases EReal.lt_iff_exists_real_btwn.1 hx with âŸ¨y, âŸ¨sum_lt_y, y_lt_xâŸ©âŸ©; clear hx
  have keyâ‚ : limsup u f < (y - limsup v f) := by
    apply lt_of_eq_of_lt _ (EReal.sub_lt_sub_of_lt_of_le sum_lt_y (le_of_eq (Eq.refl (limsup v f)))
      (ne_of_gt v_nbot) (ne_of_lt v_ntop))
    rw [â† limsup_v_real, add_sub_cancel_right]
  have keyâ‚‚ : limsup v f < limsup v f + x - y := by
    rw [â† limsup_v_real]; norm_cast; norm_cast at y_lt_x; linarith
  apply le_of_le_of_eq (EReal.limsup_add_le_ltâ‚‚ keyâ‚ keyâ‚‚); clear keyâ‚ keyâ‚‚ y_lt_x sum_lt_y
  rw [â† limsup_v_real]; norm_cast; linarith
-/

theorem ge_iff_le_forall_real_lt (x y : EReal) : y â‰¤ x â†” âˆ€ (z : â„), (z < y) â†’ (z â‰¤ x) := by
  constructor
  Â· intros h z z_lt_y
    exact le_trans (le_of_lt z_lt_y) h
  Â· intro h
    induction' x using EReal.rec with x
    Â· apply le_of_eq
      apply (eq_bot_iff_forall_lt y).2
      intro z
      apply lt_of_not_le
      intro z_le_y
      apply not_le_of_lt (bot_lt_coe (z-1))
      specialize h (z-1)
      apply h (lt_of_lt_of_le _ z_le_y)
      norm_cast
      exact sub_one_lt z
    Â· induction' y using EReal.rec with y
      Â· exact bot_le
      Â· norm_cast
        norm_cast at h
        by_contra x_lt_y
        rcases exists_between (lt_of_not_le x_lt_y) with âŸ¨z, âŸ¨x_lt_z, z_lt_yâŸ©âŸ©
        specialize h z z_lt_y
        exact not_le_of_lt x_lt_z h
      Â· exfalso
        specialize h (x + 1) (coe_lt_top (x+1))
        norm_cast at h
        exact not_le_of_lt (lt_add_one x) h
    Â· exact le_top

lemma liminf_add_ge_gtâ‚‚ {Î± : Type _} {f : Filter Î±} {u v : Î± â†’ EReal} {a b : EReal}
    (ha : a < liminf u f) (hb : b < liminf v f) :
    a + b â‰¤ liminf (u + v) f := by
  rcases eq_or_neBot f with (rfl | _); simp only [liminf_bot, le_top]
  rw [â† @liminf_const EReal Î± _ f _ (a+b)]
  apply liminf_le_liminf
  apply Eventually.mp (Eventually.and
    (eventually_lt_of_lt_liminf ha) (eventually_lt_of_lt_liminf hb))
  apply eventually_of_forall
  intros x
  simp only [Pi.add_apply, and_imp]
  exact fun ux_lt_a vx_lt_b â†¦ add_le_add (le_of_lt ux_lt_a) (le_of_lt vx_lt_b)

lemma liminf_add_top_ne_bot {Î± : Type _} {f : Filter Î±} {u : Î± â†’ EReal} {v : Î± â†’ EReal}
    (h : liminf u f = âŠ¤) (h' : liminf v f â‰  âŠ¥) :
    liminf (u + v) f = âŠ¤ := by
  apply top_le_iff.1
  apply (EReal.ge_iff_le_forall_real_lt (liminf (u+v) f) âŠ¤).2
  intro x
  rcases EReal.exists_between_coe_real (Ne.bot_lt h') with âŸ¨y, âŸ¨_, hyâŸ©âŸ©
  intro trash; clear trash
  rw [â† sub_add_cancel x y, EReal.coe_add (x-y) y, EReal.coe_sub x y]
  apply @EReal.liminf_add_ge_gtâ‚‚ Î± f u v (x-y) y _ hy
  rw [h, â† EReal.coe_sub x y]
  exact EReal.coe_lt_top (x-y)

theorem add_liminf_le_liminf_add {Î± : Type _} {f : Filter Î±} {u v : Î± â†’ EReal}
    (h : liminf u f â‰  âŠ¥ âˆ¨ liminf v f â‰  âŠ¤) (h' : liminf u f â‰  âŠ¤ âˆ¨ liminf v f â‰  âŠ¥) :
    (liminf u f) + (liminf v f) â‰¤ liminf (u + v) f:= by
  /- WLOG, liminf v f < âŠ¤. -/
  rcases eq_top_or_lt_top (liminf v f) with (v_top | v_ntop)
  Â· rcases h with (u_nbot | v_ntop)
    Â· rw [add_comm u v, EReal.liminf_add_top_ne_bot v_top u_nbot]; exact le_top
    Â· exfalso; exact v_ntop v_top
  clear h h'
  /- WLOG, âŠ¥ < liminf v f. -/
  rcases eq_bot_or_bot_lt (liminf v f) with (v_bot | v_nbot)
  Â· rw [v_bot, EReal.add_bot]; exact bot_le
  /- General case. -/
  have liminf_v_real := EReal.coe_toReal (ne_of_lt v_ntop) (ne_of_gt v_nbot)
  apply (EReal.ge_iff_le_forall_real_lt _ _).2
  intros x hx
  rcases EReal.lt_iff_exists_real_btwn.1 hx with âŸ¨y, âŸ¨x_lt_y, y_lt_sumâŸ©âŸ©; clear hx
  have keyâ‚ : (y - liminf v f) < liminf u f := by
    apply lt_of_lt_of_eq (EReal.sub_lt_sub_of_lt_of_le y_lt_sum (le_of_eq (Eq.refl (liminf v f)))
      (ne_of_gt v_nbot) (ne_of_lt v_ntop))
    rw [â† liminf_v_real, add_sub_cancel_right] -- Wait for the PR
  have keyâ‚‚ : liminf v f + x - y < liminf v f := by
    rw [â† liminf_v_real]
    norm_cast
    norm_cast at x_lt_y
    linarith
  apply le_of_eq_of_le _ (EReal.liminf_add_ge_gtâ‚‚ keyâ‚ keyâ‚‚); clear keyâ‚ keyâ‚‚ x_lt_y y_lt_sum
  rw [â† liminf_v_real]
  norm_cast
  linarith

theorem limsup_le_iff {Î± : Type _} {f : Filter Î±} {u : Î± â†’ EReal} {b : EReal} :
    limsup u f â‰¤ b â†” âˆ€ c : â„, b < c â†’ âˆ€á¶  a : Î± in f, u a â‰¤ c := by
  rw [EReal.le_iff_le_forall_real_gt] -- Wait for the PR
  constructor
  Â· intro h c b_lt_c
    rcases EReal.exists_between_coe_real b_lt_c with âŸ¨d, b_lt_d, d_lt_câŸ©
    specialize h d b_lt_d
    have key := Filter.eventually_lt_of_limsup_lt (lt_of_le_of_lt h d_lt_c)
    apply Filter.mem_of_superset key
    simp only [Set.setOf_subset_setOf]
    exact fun a h' â†¦ le_of_lt h'
  Â· intro h c b_lt_c
    rcases eq_or_neBot f with (rfl | _)
    Â· simp only [limsup_bot, bot_le]
    Â· specialize h c b_lt_c
      rw [â† @Filter.limsup_const EReal Î± _ f _ (c : EReal)]
      exact limsup_le_limsup h

theorem limsup_le_const_forall {Î± : Type _} {f : Filter Î±} {u : Î± â†’ EReal} {b : EReal}
    (h : âˆ€ a : Î±, u a â‰¤ b) :
    limsup u f â‰¤ b := by
  apply EReal.limsup_le_iff.2
  exact fun c b_lt_c â†¦ eventually_of_forall (fun a : Î± â†¦ le_trans (h a) (le_of_lt b_lt_c))

theorem const_le_limsup_forall {Î± : Type _} {f : Filter Î±} [NeBot f] {u : Î± â†’ EReal}
    {b : EReal} (h : âˆ€ a : Î±, b â‰¤ u a) :
    b â‰¤ limsup u f := by
  rw [â† @Filter.limsup_const EReal Î± _ f _ b]
  exact limsup_le_limsup (eventually_of_forall h)

theorem liminf_le_const_forall {Î± : Type _} {f : Filter Î±} [NeBot f] {u : Î± â†’ EReal}
    {b : EReal} (h : âˆ€ a : Î±, u a â‰¤ b) :
    liminf u f â‰¤ b := by
  rw [â† @Filter.liminf_const EReal Î± _ f _ b]
  exact liminf_le_liminf (eventually_of_forall h)

theorem const_le_liminf_forall {Î± : Type _} {f : Filter Î±} {u : Î± â†’ EReal} {b : EReal}
    (h : âˆ€ a : Î±, b â‰¤ u a) :
    b â‰¤ liminf u f := by
  rcases eq_or_neBot f with (rfl | _)
  Â· simp only [liminf_bot, le_top]
  Â· rw [â† @Filter.liminf_const EReal Î± _ f _ b]
    exact liminf_le_liminf (eventually_of_forall h)

theorem limsup_max {Î± : Type _} {f : Filter Î±} {u v : Î± â†’ EReal} :
    limsup (fun a â†¦ max (u a) (v a)) f = max (limsup u f) (limsup v f) := by
  rcases eq_or_neBot f with (rfl | _); simp [limsup_bot]
  apply le_antisymm
  Â· apply EReal.limsup_le_iff.2
    intro b hb
    have hu := Filter.eventually_lt_of_limsup_lt (lt_of_le_of_lt (le_max_left _ _) hb)
    have hv := Filter.eventually_lt_of_limsup_lt (lt_of_le_of_lt (le_max_right _ _) hb); clear hb
    apply Filter.mem_of_superset (Filter.inter_mem hu hv); clear hu hv
    intro a
    simp only [Set.mem_inter_iff, Set.mem_setOf_eq, max_le_iff, and_imp]
    exact fun hua hva â†¦ âŸ¨le_of_lt hua, le_of_lt hvaâŸ©
  Â· apply max_le
    Â· exact limsup_le_limsup (eventually_of_forall (fun a : Î± â†¦ le_max_left (u a) (v a)))
    Â· exact limsup_le_limsup (eventually_of_forall (fun a : Î± â†¦ le_max_right (u a) (v a)))

/-! ### Continuity of addition -/

theorem continuousAt_add_coe_coe (a b : â„) :
    ContinuousAt (fun p : EReal Ã— EReal => p.1 + p.2) (a, b) := by
  simp only [ContinuousAt, nhds_coe_coe, â† coe_add, tendsto_map'_iff, (Â· âˆ˜ Â·), tendsto_coe,
    tendsto_add]
#align ereal.continuous_at_add_coe_coe EReal.continuousAt_add_coe_coe

theorem continuousAt_add_top_coe (a : â„) :
    ContinuousAt (fun p : EReal Ã— EReal => p.1 + p.2) (âŠ¤, a) := by
  simp only [ContinuousAt, tendsto_nhds_top_iff_real, top_add_coe]
  refine fun r â†¦ ((lt_mem_nhds (coe_lt_top (r - (a - 1)))).prod_nhds
    (lt_mem_nhds <| EReal.coe_lt_coe_iff.2 <| sub_one_lt _)).mono fun _ h â†¦ ?_
  simpa only [â† coe_add, sub_add_cancel] using add_lt_add h.1 h.2
#align ereal.continuous_at_add_top_coe EReal.continuousAt_add_top_coe

theorem continuousAt_add_coe_top (a : â„) :
    ContinuousAt (fun p : EReal Ã— EReal => p.1 + p.2) (a, âŠ¤) := by
  simpa only [add_comm, (Â· âˆ˜ Â·), ContinuousAt, Prod.swap]
    using Tendsto.comp (continuousAt_add_top_coe a) (continuous_swap.tendsto ((a : EReal), âŠ¤))
#align ereal.continuous_at_add_coe_top EReal.continuousAt_add_coe_top

theorem continuousAt_add_top_top : ContinuousAt (fun p : EReal Ã— EReal => p.1 + p.2) (âŠ¤, âŠ¤) := by
  simp only [ContinuousAt, tendsto_nhds_top_iff_real, top_add_top]
  refine fun r â†¦ ((lt_mem_nhds (coe_lt_top 0)).prod_nhds
    (lt_mem_nhds <| coe_lt_top r)).mono fun _ h â†¦ ?_
  simpa only [coe_zero, zero_add] using add_lt_add h.1 h.2
#align ereal.continuous_at_add_top_top EReal.continuousAt_add_top_top

theorem continuousAt_add_bot_coe (a : â„) :
    ContinuousAt (fun p : EReal Ã— EReal => p.1 + p.2) (âŠ¥, a) := by
  simp only [ContinuousAt, tendsto_nhds_bot_iff_real, bot_add]
  refine fun r â†¦ ((gt_mem_nhds (bot_lt_coe (r - (a + 1)))).prod_nhds
    (gt_mem_nhds <| EReal.coe_lt_coe_iff.2 <| lt_add_one _)).mono fun _ h â†¦ ?_
  simpa only [â† coe_add, sub_add_cancel] using add_lt_add h.1 h.2
#align ereal.continuous_at_add_bot_coe EReal.continuousAt_add_bot_coe

theorem continuousAt_add_coe_bot (a : â„) :
    ContinuousAt (fun p : EReal Ã— EReal => p.1 + p.2) (a, âŠ¥) := by
  simpa only [add_comm, (Â· âˆ˜ Â·), ContinuousAt, Prod.swap]
    using Tendsto.comp (continuousAt_add_bot_coe a) (continuous_swap.tendsto ((a : EReal), âŠ¥))
#align ereal.continuous_at_add_coe_bot EReal.continuousAt_add_coe_bot

theorem continuousAt_add_bot_bot : ContinuousAt (fun p : EReal Ã— EReal => p.1 + p.2) (âŠ¥, âŠ¥) := by
  simp only [ContinuousAt, tendsto_nhds_bot_iff_real, bot_add]
  refine fun r â†¦ ((gt_mem_nhds (bot_lt_coe 0)).prod_nhds
    (gt_mem_nhds <| bot_lt_coe r)).mono fun _ h â†¦ ?_
  simpa only [coe_zero, zero_add] using add_lt_add h.1 h.2
#align ereal.continuous_at_add_bot_bot EReal.continuousAt_add_bot_bot

/-- The addition on `EReal` is continuous except where it doesn't make sense (i.e., at `(âŠ¥, âŠ¤)`
and at `(âŠ¤, âŠ¥)`). -/
theorem continuousAt_add {p : EReal Ã— EReal} (h : p.1 â‰  âŠ¤ âˆ¨ p.2 â‰  âŠ¥) (h' : p.1 â‰  âŠ¥ âˆ¨ p.2 â‰  âŠ¤) :
    ContinuousAt (fun p : EReal Ã— EReal => p.1 + p.2) p := by
  rcases p with âŸ¨x, yâŸ©
  induction x <;> induction y
  Â· exact continuousAt_add_bot_bot
  Â· exact continuousAt_add_bot_coe _
  Â· simp at h'
  Â· exact continuousAt_add_coe_bot _
  Â· exact continuousAt_add_coe_coe _ _
  Â· exact continuousAt_add_coe_top _
  Â· simp at h
  Â· exact continuousAt_add_top_coe _
  Â· exact continuousAt_add_top_top
#align ereal.continuous_at_add EReal.continuousAt_add

/-! ### Negation -/

instance : ContinuousNeg EReal := âŸ¨negOrderIso.continuousâŸ©

/-- Negation on `EReal` as a homeomorphism -/
@[deprecated Homeomorph.neg (since := "2023-03-14")]
def negHomeo : EReal â‰ƒâ‚œ EReal :=
  negOrderIso.toHomeomorph
#align ereal.neg_homeo EReal.negHomeo

@[deprecated continuous_neg (since := "2023-03-14")]
protected theorem continuous_neg : Continuous fun x : EReal => -x :=
  continuous_neg
#align ereal.continuous_neg EReal.continuous_neg

end EReal
