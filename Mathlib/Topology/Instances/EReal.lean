/-
Copyright (c) 2021 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Data.Rat.Encodable
import Mathlib.Data.Real.EReal
import Mathlib.Topology.Instances.ENNReal
import Mathlib.Topology.Order.MonotoneContinuity

#align_import topology.instances.ereal from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Topological structure on `EReal`

We endow `EReal` with the order topology, and prove basic properties of this topology.

## Main results

* `Real.toEReal : ‚Ñù ‚Üí EReal` is an open embedding
* `ENNReal.toEReal : ‚Ñù‚â•0‚àû ‚Üí EReal` is a closed embedding
* The addition on `EReal` is continuous except at `(‚ä•, ‚ä§)` and at `(‚ä§, ‚ä•)`.
* Negation is a homeomorphism on `EReal`.

## Implementation

Most proofs are adapted from the corresponding proofs on `‚Ñù‚â•0‚àû`.
-/


noncomputable section

open scoped Classical
open Set Filter Metric TopologicalSpace Topology
open scoped ENNReal NNReal Filter

variable {Œ± : Type*} [TopologicalSpace Œ±]

namespace EReal

instance : TopologicalSpace EReal := Preorder.topology EReal
instance : OrderTopology EReal := ‚ü®rfl‚ü©
instance : T5Space EReal := inferInstance
instance : T2Space EReal := inferInstance

lemma denseRange_ratCast : DenseRange (fun r : ‚Ñö ‚Ü¶ ((r : ‚Ñù) : EReal)) :=
  dense_of_exists_between fun _ _ h => exists_range_iff.2 <| exists_rat_btwn_of_lt h

instance : SecondCountableTopology EReal :=
  have : SeparableSpace EReal := ‚ü®‚ü®_, countable_range _, denseRange_ratCast‚ü©‚ü©
  .of_separableSpace_orderTopology _

/-! ### Real coercion -/

theorem embedding_coe : Embedding ((‚Üë) : ‚Ñù ‚Üí EReal) :=
  coe_strictMono.embedding_of_ordConnected <| by rw [range_coe_eq_Ioo]; exact ordConnected_Ioo
#align ereal.embedding_coe EReal.embedding_coe

theorem openEmbedding_coe : OpenEmbedding ((‚Üë) : ‚Ñù ‚Üí EReal) :=
  ‚ü®embedding_coe, by simp only [range_coe_eq_Ioo, isOpen_Ioo]‚ü©
#align ereal.open_embedding_coe EReal.openEmbedding_coe

@[norm_cast]
theorem tendsto_coe {Œ± : Type*} {f : Filter Œ±} {m : Œ± ‚Üí ‚Ñù} {a : ‚Ñù} :
    Tendsto (fun a => (m a : EReal)) f (ùìù ‚Üëa) ‚Üî Tendsto m f (ùìù a) :=
  embedding_coe.tendsto_nhds_iff.symm
#align ereal.tendsto_coe EReal.tendsto_coe

theorem _root_.continuous_coe_real_ereal : Continuous ((‚Üë) : ‚Ñù ‚Üí EReal) :=
  embedding_coe.continuous
#align continuous_coe_real_ereal continuous_coe_real_ereal

theorem continuous_coe_iff {f : Œ± ‚Üí ‚Ñù} : (Continuous fun a => (f a : EReal)) ‚Üî Continuous f :=
  embedding_coe.continuous_iff.symm
#align ereal.continuous_coe_iff EReal.continuous_coe_iff

theorem nhds_coe {r : ‚Ñù} : ùìù (r : EReal) = (ùìù r).map (‚Üë) :=
  (openEmbedding_coe.map_nhds_eq r).symm
#align ereal.nhds_coe EReal.nhds_coe

theorem nhds_coe_coe {r p : ‚Ñù} :
    ùìù ((r : EReal), (p : EReal)) = (ùìù (r, p)).map fun p : ‚Ñù √ó ‚Ñù => (‚Üëp.1, ‚Üëp.2) :=
  ((openEmbedding_coe.prod openEmbedding_coe).map_nhds_eq (r, p)).symm
#align ereal.nhds_coe_coe EReal.nhds_coe_coe

theorem tendsto_toReal {a : EReal} (ha : a ‚â† ‚ä§) (h'a : a ‚â† ‚ä•) :
    Tendsto EReal.toReal (ùìù a) (ùìù a.toReal) := by
  lift a to ‚Ñù using ‚ü®ha, h'a‚ü©
  rw [nhds_coe, tendsto_map'_iff]
  exact tendsto_id
#align ereal.tendsto_to_real EReal.tendsto_toReal

theorem continuousOn_toReal : ContinuousOn EReal.toReal ({‚ä•, ‚ä§}·∂ú : Set EReal) := fun _a ha =>
  ContinuousAt.continuousWithinAt (tendsto_toReal (mt Or.inr ha) (mt Or.inl ha))
#align ereal.continuous_on_to_real EReal.continuousOn_toReal

/-- The set of finite `EReal` numbers is homeomorphic to `‚Ñù`. -/
def neBotTopHomeomorphReal : ({‚ä•, ‚ä§}·∂ú : Set EReal) ‚âÉ‚Çú ‚Ñù where
  toEquiv := neTopBotEquivReal
  continuous_toFun := continuousOn_iff_continuous_restrict.1 continuousOn_toReal
  continuous_invFun := continuous_coe_real_ereal.subtype_mk _
#align ereal.ne_bot_top_homeomorph_real EReal.neBotTopHomeomorphReal

/-! ### ENNReal coercion -/

theorem embedding_coe_ennreal : Embedding ((‚Üë) : ‚Ñù‚â•0‚àû ‚Üí EReal) :=
  coe_ennreal_strictMono.embedding_of_ordConnected <| by
    rw [range_coe_ennreal]; exact ordConnected_Ici
#align ereal.embedding_coe_ennreal EReal.embedding_coe_ennreal

theorem closedEmbedding_coe_ennreal : ClosedEmbedding ((‚Üë) : ‚Ñù‚â•0‚àû ‚Üí EReal) :=
  ‚ü®embedding_coe_ennreal, by rw [range_coe_ennreal]; exact isClosed_Ici‚ü©

@[norm_cast]
theorem tendsto_coe_ennreal {Œ± : Type*} {f : Filter Œ±} {m : Œ± ‚Üí ‚Ñù‚â•0‚àû} {a : ‚Ñù‚â•0‚àû} :
    Tendsto (fun a => (m a : EReal)) f (ùìù ‚Üëa) ‚Üî Tendsto m f (ùìù a) :=
  embedding_coe_ennreal.tendsto_nhds_iff.symm
#align ereal.tendsto_coe_ennreal EReal.tendsto_coe_ennreal

theorem _root_.continuous_coe_ennreal_ereal : Continuous ((‚Üë) : ‚Ñù‚â•0‚àû ‚Üí EReal) :=
  embedding_coe_ennreal.continuous
#align continuous_coe_ennreal_ereal continuous_coe_ennreal_ereal

theorem continuous_coe_ennreal_iff {f : Œ± ‚Üí ‚Ñù‚â•0‚àû} :
    (Continuous fun a => (f a : EReal)) ‚Üî Continuous f :=
  embedding_coe_ennreal.continuous_iff.symm
#align ereal.continuous_coe_ennreal_iff EReal.continuous_coe_ennreal_iff

/-! ### Neighborhoods of infinity -/

theorem nhds_top : ùìù (‚ä§ : EReal) = ‚®Ö (a) (_ : a ‚â† ‚ä§), ùìü (Ioi a) :=
  nhds_top_order.trans <| by simp only [lt_top_iff_ne_top]
#align ereal.nhds_top EReal.nhds_top

nonrec theorem nhds_top_basis : (ùìù (‚ä§ : EReal)).HasBasis (fun _ : ‚Ñù ‚Ü¶ True) (Ioi ¬∑) := by
  refine nhds_top_basis.to_hasBasis (fun x hx => ?_) fun _ _ ‚Ü¶ ‚ü®_, coe_lt_top _, Subset.rfl‚ü©
  rcases exists_rat_btwn_of_lt hx with ‚ü®y, hxy, -‚ü©
  exact ‚ü®_, trivial, Ioi_subset_Ioi hxy.le‚ü©

theorem nhds_top' : ùìù (‚ä§ : EReal) = ‚®Ö a : ‚Ñù, ùìü (Ioi ‚Üëa) := nhds_top_basis.eq_iInf
#align ereal.nhds_top' EReal.nhds_top'

theorem mem_nhds_top_iff {s : Set EReal} : s ‚àà ùìù (‚ä§ : EReal) ‚Üî ‚àÉ y : ‚Ñù, Ioi (y : EReal) ‚äÜ s :=
  nhds_top_basis.mem_iff.trans <| by simp only [true_and]
#align ereal.mem_nhds_top_iff EReal.mem_nhds_top_iff

theorem tendsto_nhds_top_iff_real {Œ± : Type*} {m : Œ± ‚Üí EReal} {f : Filter Œ±} :
    Tendsto m f (ùìù ‚ä§) ‚Üî ‚àÄ x : ‚Ñù, ‚àÄ·∂† a in f, ‚Üëx < m a :=
  nhds_top_basis.tendsto_right_iff.trans <| by simp only [true_implies, mem_Ioi]
#align ereal.tendsto_nhds_top_iff_real EReal.tendsto_nhds_top_iff_real

theorem nhds_bot : ùìù (‚ä• : EReal) = ‚®Ö (a) (_ : a ‚â† ‚ä•), ùìü (Iio a) :=
  nhds_bot_order.trans <| by simp only [bot_lt_iff_ne_bot]
#align ereal.nhds_bot EReal.nhds_bot

theorem nhds_bot_basis : (ùìù (‚ä• : EReal)).HasBasis (fun _ : ‚Ñù ‚Ü¶ True) (Iio ¬∑) := by
  refine _root_.nhds_bot_basis.to_hasBasis (fun x hx => ?_) fun _ _ ‚Ü¶ ‚ü®_, bot_lt_coe _, Subset.rfl‚ü©
  rcases exists_rat_btwn_of_lt hx with ‚ü®y, -, hxy‚ü©
  exact ‚ü®_, trivial, Iio_subset_Iio hxy.le‚ü©

theorem nhds_bot' : ùìù (‚ä• : EReal) = ‚®Ö a : ‚Ñù, ùìü (Iio ‚Üëa) :=
  nhds_bot_basis.eq_iInf
#align ereal.nhds_bot' EReal.nhds_bot'

theorem mem_nhds_bot_iff {s : Set EReal} : s ‚àà ùìù (‚ä• : EReal) ‚Üî ‚àÉ y : ‚Ñù, Iio (y : EReal) ‚äÜ s :=
  nhds_bot_basis.mem_iff.trans <| by simp only [true_and]
#align ereal.mem_nhds_bot_iff EReal.mem_nhds_bot_iff

theorem tendsto_nhds_bot_iff_real {Œ± : Type*} {m : Œ± ‚Üí EReal} {f : Filter Œ±} :
    Tendsto m f (ùìù ‚ä•) ‚Üî ‚àÄ x : ‚Ñù, ‚àÄ·∂† a in f, m a < x :=
  nhds_bot_basis.tendsto_right_iff.trans <| by simp only [true_implies, mem_Iio]
#align ereal.tendsto_nhds_bot_iff_real EReal.tendsto_nhds_bot_iff_real

/-! ### Continuity of addition -/

theorem continuousAt_add_coe_coe (a b : ‚Ñù) :
    ContinuousAt (fun p : EReal √ó EReal => p.1 + p.2) (a, b) := by
  simp only [ContinuousAt, nhds_coe_coe, ‚Üê coe_add, tendsto_map'_iff, (¬∑ ‚àò ¬∑), tendsto_coe,
    tendsto_add]
#align ereal.continuous_at_add_coe_coe EReal.continuousAt_add_coe_coe

theorem continuousAt_add_top_coe (a : ‚Ñù) :
    ContinuousAt (fun p : EReal √ó EReal => p.1 + p.2) (‚ä§, a) := by
  simp only [ContinuousAt, tendsto_nhds_top_iff_real, top_add_coe]
  refine fun r ‚Ü¶ ((lt_mem_nhds (coe_lt_top (r - (a - 1)))).prod_nhds
    (lt_mem_nhds <| EReal.coe_lt_coe_iff.2 <| sub_one_lt _)).mono fun _ h ‚Ü¶ ?_
  simpa only [‚Üê coe_add, sub_add_cancel] using add_lt_add h.1 h.2
#align ereal.continuous_at_add_top_coe EReal.continuousAt_add_top_coe

theorem continuousAt_add_coe_top (a : ‚Ñù) :
    ContinuousAt (fun p : EReal √ó EReal => p.1 + p.2) (a, ‚ä§) := by
  simpa only [add_comm, (¬∑ ‚àò ¬∑), ContinuousAt, Prod.swap]
    using Tendsto.comp (continuousAt_add_top_coe a) (continuous_swap.tendsto ((a : EReal), ‚ä§))
#align ereal.continuous_at_add_coe_top EReal.continuousAt_add_coe_top

theorem continuousAt_add_top_top : ContinuousAt (fun p : EReal √ó EReal => p.1 + p.2) (‚ä§, ‚ä§) := by
  simp only [ContinuousAt, tendsto_nhds_top_iff_real, top_add_top]
  refine fun r ‚Ü¶ ((lt_mem_nhds (coe_lt_top 0)).prod_nhds
    (lt_mem_nhds <| coe_lt_top r)).mono fun _ h ‚Ü¶ ?_
  simpa only [coe_zero, zero_add] using add_lt_add h.1 h.2
#align ereal.continuous_at_add_top_top EReal.continuousAt_add_top_top

theorem continuousAt_add_bot_coe (a : ‚Ñù) :
    ContinuousAt (fun p : EReal √ó EReal => p.1 + p.2) (‚ä•, a) := by
  simp only [ContinuousAt, tendsto_nhds_bot_iff_real, bot_add]
  refine fun r ‚Ü¶ ((gt_mem_nhds (bot_lt_coe (r - (a + 1)))).prod_nhds
    (gt_mem_nhds <| EReal.coe_lt_coe_iff.2 <| lt_add_one _)).mono fun _ h ‚Ü¶ ?_
  simpa only [‚Üê coe_add, sub_add_cancel] using add_lt_add h.1 h.2
#align ereal.continuous_at_add_bot_coe EReal.continuousAt_add_bot_coe

theorem continuousAt_add_coe_bot (a : ‚Ñù) :
    ContinuousAt (fun p : EReal √ó EReal => p.1 + p.2) (a, ‚ä•) := by
  simpa only [add_comm, (¬∑ ‚àò ¬∑), ContinuousAt, Prod.swap]
    using Tendsto.comp (continuousAt_add_bot_coe a) (continuous_swap.tendsto ((a : EReal), ‚ä•))
#align ereal.continuous_at_add_coe_bot EReal.continuousAt_add_coe_bot

theorem continuousAt_add_bot_bot : ContinuousAt (fun p : EReal √ó EReal => p.1 + p.2) (‚ä•, ‚ä•) := by
  simp only [ContinuousAt, tendsto_nhds_bot_iff_real, bot_add]
  refine fun r ‚Ü¶ ((gt_mem_nhds (bot_lt_coe 0)).prod_nhds
    (gt_mem_nhds <| bot_lt_coe r)).mono fun _ h ‚Ü¶ ?_
  simpa only [coe_zero, zero_add] using add_lt_add h.1 h.2
#align ereal.continuous_at_add_bot_bot EReal.continuousAt_add_bot_bot

/-- The addition on `EReal` is continuous except where it doesn't make sense (i.e., at `(‚ä•, ‚ä§)`
and at `(‚ä§, ‚ä•)`). -/
theorem continuousAt_add {p : EReal √ó EReal} (h : p.1 ‚â† ‚ä§ ‚à® p.2 ‚â† ‚ä•) (h' : p.1 ‚â† ‚ä• ‚à® p.2 ‚â† ‚ä§) :
    ContinuousAt (fun p : EReal √ó EReal => p.1 + p.2) p := by
  rcases p with ‚ü®x, y‚ü©
  induction x <;> induction y
  ¬∑ exact continuousAt_add_bot_bot
  ¬∑ exact continuousAt_add_bot_coe _
  ¬∑ simp at h'
  ¬∑ exact continuousAt_add_coe_bot _
  ¬∑ exact continuousAt_add_coe_coe _ _
  ¬∑ exact continuousAt_add_coe_top _
  ¬∑ simp at h
  ¬∑ exact continuousAt_add_top_coe _
  ¬∑ exact continuousAt_add_top_top
#align ereal.continuous_at_add EReal.continuousAt_add

/-! ### Negation -/

instance : ContinuousNeg EReal := ‚ü®negOrderIso.continuous‚ü©

#align ereal.neg_homeo Homeomorph.neg
#align ereal.continuous_neg ContinuousNeg.continuous_neg

/-! ### Continuity of multiplication
Outside of indeterminacies `(0, ¬±‚àû)` and `(¬±‚àû, 0)`, the multiplication on `EReal` is continuous.
There are many different cases to consider, so we first prove some special cases and leverage as
much as possible the symmetries of the multiplication. -/

private lemma continuousAt_mul_swap {a b : EReal}
    (h : ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) (a, b)) :
    ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) (b, a) := by
  have : (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) = (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2)
      ‚àò Prod.swap := by
    ext p
    simp only [Function.comp_apply, Prod.fst_swap, Prod.snd_swap]
    exact mul_comm p.1 p.2
  rw [this]
  apply ContinuousAt.comp _ (Continuous.continuousAt continuous_swap)
  simp [h]

private lemma continuousAt_mul_symm1 {a b : EReal}
    (h : ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) (a, b)) :
    ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) (-a, b) := by
  have : (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) = (fun x : EReal ‚Ü¶ -x)
      ‚àò (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) ‚àò (fun p : EReal √ó EReal ‚Ü¶ (-p.1, p.2)) := by
    ext p
    simp
  rw [this]
  apply ContinuousAt.comp (Continuous.continuousAt continuous_neg)
    <| ContinuousAt.comp _ (ContinuousAt.prod_map (Continuous.continuousAt continuous_neg)
      (Continuous.continuousAt continuous_id))
  simp [h]

private lemma continuousAt_mul_symm2 {a b : EReal}
    (h : ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) (a, b)) :
    ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) (a, -b) :=
  continuousAt_mul_swap (continuousAt_mul_symm1 (continuousAt_mul_swap h))

private lemma continuousAt_mul_symm3 {a b : EReal}
    (h : ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) (a, b)) :
    ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) (-a, -b) :=
  continuousAt_mul_symm1 (continuousAt_mul_symm2 h)

private lemma continuousAt_mul_coe_coe (a b : ‚Ñù) :
    ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) (a, b) := by
  simp [ContinuousAt, EReal.nhds_coe_coe, ‚Üê EReal.coe_mul, Filter.tendsto_map'_iff,
    (¬∑ ‚àò ¬∑), EReal.tendsto_coe, tendsto_mul]

private lemma continuousAt_mul_top_top :
    ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) (‚ä§, ‚ä§) := by
  simp only [ContinuousAt, EReal.top_mul_top, EReal.tendsto_nhds_top_iff_real]
  intro x
  rw [_root_.eventually_nhds_iff]
  use (Set.Ioi ((max x 0) : EReal)) √óÀ¢ (Set.Ioi 1)
  split_ands
  . intros p p_in_prod
    simp only [Set.mem_prod, Set.mem_Ioi, max_lt_iff] at p_in_prod
    rcases p_in_prod with ‚ü®‚ü®p1_gt_x, p1_pos‚ü©, p2_gt_1‚ü©
    have := mul_le_mul_of_nonneg_left (le_of_lt p2_gt_1) (le_of_lt p1_pos)
    rw [mul_one p.1] at this
    exact lt_of_lt_of_le p1_gt_x this
  . exact IsOpen.prod isOpen_Ioi isOpen_Ioi
  . simp
  . rw [Set.mem_Ioi, ‚Üê EReal.coe_one]; exact EReal.coe_lt_top 1

private lemma continuousAt_mul_top_pos {a : ‚Ñù} (h : 0 < a) :
    ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) (‚ä§, a) := by
  simp only [ContinuousAt, EReal.top_mul_coe_of_pos h, EReal.tendsto_nhds_top_iff_real]
  intro x
  rw [_root_.eventually_nhds_iff]
  use (Set.Ioi ((2*(max (x+1) 0)/a : ‚Ñù) : EReal)) √óÀ¢ (Set.Ioi ((a/2 : ‚Ñù) : EReal))
  split_ands
  . intros p p_in_prod
    simp only [Set.mem_prod, Set.mem_Ioi] at p_in_prod
    rcases p_in_prod with ‚ü®p1_gt, p2_gt‚ü©
    have p1_pos : 0 < p.1 := by
      apply lt_of_le_of_lt _ p1_gt
      rw [EReal.coe_nonneg]
      apply mul_nonneg _ (le_of_lt (inv_pos_of_pos h))
      simp only [gt_iff_lt, Nat.ofNat_pos, mul_nonneg_iff_of_pos_left, le_max_iff, le_refl, or_true]
    have a2_pos : 0 < ((a/2 : ‚Ñù) : EReal) := by rw [EReal.coe_pos]; linarith
    have lock := mul_le_mul_of_nonneg_right (le_of_lt p1_gt) (le_of_lt a2_pos)
    have key := mul_le_mul_of_nonneg_left (le_of_lt p2_gt) (le_of_lt p1_pos)
    replace lock := le_trans lock key
    apply lt_of_lt_of_le _ lock
    rw [‚Üê EReal.coe_mul, EReal.coe_lt_coe_iff, div_mul_div_comm, mul_comm,
      ‚Üê div_mul_div_comm, mul_div_right_comm]
    simp only [ne_eq, Ne.symm (ne_of_lt h), not_false_eq_true, _root_.div_self, OfNat.ofNat_ne_zero,
      one_mul, lt_max_iff, lt_add_iff_pos_right, zero_lt_one, true_or]
  . exact IsOpen.prod isOpen_Ioi isOpen_Ioi
  . simp
  . simp [h]

private lemma continuousAt_mul_top_ne_zero {a : ‚Ñù} (h : a ‚â† 0) :
    ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) (‚ä§, a) := by
  rcases lt_or_gt_of_ne h with a_neg | a_pos
  . exact neg_neg a ‚ñ∏ continuousAt_mul_symm2 (continuousAt_mul_top_pos (neg_pos.2 a_neg))
  . exact continuousAt_mul_top_pos a_pos

/-- The multiplication on `EReal` is continuous except at indeterminacies
(i.e. whenever one value is zero and the other infinite). -/
theorem continuousAt_mul {p : EReal √ó EReal} (h‚ÇÅ : p.1 ‚â† 0 ‚à® p.2 ‚â† ‚ä•)
    (h‚ÇÇ : p.1 ‚â† 0 ‚à® p.2 ‚â† ‚ä§) (h‚ÇÉ : p.1 ‚â† ‚ä• ‚à® p.2 ‚â† 0) (h‚ÇÑ : p.1 ‚â† ‚ä§ ‚à® p.2 ‚â† 0) :
    ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) p := by
  rcases p with ‚ü®x, y‚ü©
  induction x <;> induction y
  . exact continuousAt_mul_symm3 continuousAt_mul_top_top
  . simp only [ne_eq, not_true_eq_false, EReal.coe_eq_zero, false_or] at h‚ÇÉ
    exact continuousAt_mul_symm1 (continuousAt_mul_top_ne_zero h‚ÇÉ)
  . exact EReal.neg_top ‚ñ∏ continuousAt_mul_symm1 continuousAt_mul_top_top
  . simp only [ne_eq, EReal.coe_eq_zero, not_true_eq_false, or_false] at h‚ÇÅ
    exact continuousAt_mul_symm2 (continuousAt_mul_swap (continuousAt_mul_top_ne_zero h‚ÇÅ))
  . exact continuousAt_mul_coe_coe _ _
  . simp only [ne_eq, EReal.coe_eq_zero, not_true_eq_false, or_false] at h‚ÇÇ
    exact continuousAt_mul_swap (continuousAt_mul_top_ne_zero h‚ÇÇ)
  . exact continuousAt_mul_symm2 continuousAt_mul_top_top
  . simp only [ne_eq, not_true_eq_false, EReal.coe_eq_zero, false_or] at h‚ÇÑ
    exact continuousAt_mul_top_ne_zero h‚ÇÑ
  . exact continuousAt_mul_top_top

end EReal
