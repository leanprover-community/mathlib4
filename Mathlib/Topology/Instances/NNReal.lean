/-
Copyright (c) 2018 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin
-/
import Mathlib.Topology.Algebra.InfiniteSum.Order
import Mathlib.Topology.Algebra.InfiniteSum.Ring
import Mathlib.Topology.Instances.Real
import Mathlib.Topology.MetricSpace.Isometry

#align_import topology.instances.nnreal from "leanprover-community/mathlib"@"32253a1a1071173b33dc7d6a218cf722c6feb514"

/-!
# Topology on `‚Ñù‚â•0`

The natural topology on `‚Ñù‚â•0` (the one induced from `‚Ñù`), and a basic API.

## Main definitions

Instances for the following typeclasses are defined:

* `TopologicalSpace ‚Ñù‚â•0`
* `TopologicalSemiring ‚Ñù‚â•0`
* `SecondCountableTopology ‚Ñù‚â•0`
* `OrderTopology ‚Ñù‚â•0`
* `ProperSpace ‚Ñù‚â•0`
* `ContinuousSub ‚Ñù‚â•0`
* `HasContinuousInv‚ÇÄ ‚Ñù‚â•0` (continuity of `x‚Åª¬π` away from `0`)
* `ContinuousSMul ‚Ñù‚â•0 Œ±` (whenever `Œ±` has a continuous `MulAction ‚Ñù Œ±`)

Everything is inherited from the corresponding structures on the reals.

## Main statements

Various mathematically trivial lemmas are proved about the compatibility
of limits and sums in `‚Ñù‚â•0` and `‚Ñù`. For example

* `tendsto_coe {f : Filter Œ±} {m : Œ± ‚Üí ‚Ñù‚â•0} {x : ‚Ñù‚â•0} :
  Filter.Tendsto (fun a, (m a : ‚Ñù)) f (ùìù (x : ‚Ñù)) ‚Üî Filter.Tendsto m f (ùìù x)`

says that the limit of a filter along a map to `‚Ñù‚â•0` is the same in `‚Ñù` and `‚Ñù‚â•0`, and

* `coe_tsum {f : Œ± ‚Üí ‚Ñù‚â•0} : ((‚àë'a, f a) : ‚Ñù) = (‚àë'a, (f a : ‚Ñù))`

says that says that a sum of elements in `‚Ñù‚â•0` is the same in `‚Ñù` and `‚Ñù‚â•0`.

Similarly, some mathematically trivial lemmas about infinite sums are proved,
a few of which rely on the fact that subtraction is continuous.

-/


noncomputable section

open Set TopologicalSpace Metric Filter

open Topology

namespace NNReal

open NNReal Filter

instance : TopologicalSpace ‚Ñù‚â•0 := inferInstance

-- short-circuit type class inference
instance : TopologicalSemiring ‚Ñù‚â•0 where
  toContinuousAdd := continuousAdd_induced toRealHom
  toContinuousMul := continuousMul_induced toRealHom

instance : SecondCountableTopology ‚Ñù‚â•0 :=
  inferInstanceAs (SecondCountableTopology { x : ‚Ñù | 0 ‚â§ x })

instance : OrderTopology ‚Ñù‚â•0 :=
  orderTopology_of_ordConnected (t := Ici 0)

instance : CompleteSpace ‚Ñù‚â•0 :=
  isClosed_Ici.completeSpace_coe

instance : ContinuousStar ‚Ñù‚â•0 where
  continuous_star := continuous_id
section coe

variable {Œ± : Type*}

open Filter Finset

theorem _root_.continuous_real_toNNReal : Continuous Real.toNNReal :=
  (continuous_id.max continuous_const).subtype_mk _
#align continuous_real_to_nnreal continuous_real_toNNReal

/-- `Real.toNNReal` bundled as a continuous map for convenience. -/
@[simps (config := .asFn)]
noncomputable def _root_.ContinuousMap.realToNNReal : C(‚Ñù, ‚Ñù‚â•0) :=
  .mk Real.toNNReal continuous_real_toNNReal

theorem continuous_coe : Continuous ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) :=
  continuous_subtype_val
#align nnreal.continuous_coe NNReal.continuous_coe

/-- Embedding of `‚Ñù‚â•0` to `‚Ñù` as a bundled continuous map. -/
@[simps (config := .asFn)]
def _root_.ContinuousMap.coeNNRealReal : C(‚Ñù‚â•0, ‚Ñù) :=
  ‚ü®(‚Üë), continuous_coe‚ü©
#align continuous_map.coe_nnreal_real ContinuousMap.coeNNRealReal
#align continuous_map.coe_nnreal_real_apply ContinuousMap.coeNNRealReal_apply

instance ContinuousMap.canLift {X : Type*} [TopologicalSpace X] :
    CanLift C(X, ‚Ñù) C(X, ‚Ñù‚â•0) ContinuousMap.coeNNRealReal.comp fun f => ‚àÄ x, 0 ‚â§ f x where
  prf f hf := ‚ü®‚ü®fun x => ‚ü®f x, hf x‚ü©, f.2.subtype_mk _‚ü©, DFunLike.ext' rfl‚ü©
#align nnreal.continuous_map.can_lift NNReal.ContinuousMap.canLift

@[simp, norm_cast]
theorem tendsto_coe {f : Filter Œ±} {m : Œ± ‚Üí ‚Ñù‚â•0} {x : ‚Ñù‚â•0} :
    Tendsto (fun a => (m a : ‚Ñù)) f (ùìù (x : ‚Ñù)) ‚Üî Tendsto m f (ùìù x) :=
  tendsto_subtype_rng.symm
#align nnreal.tendsto_coe NNReal.tendsto_coe

theorem tendsto_coe' {f : Filter Œ±} [NeBot f] {m : Œ± ‚Üí ‚Ñù‚â•0} {x : ‚Ñù} :
    Tendsto (fun a => m a : Œ± ‚Üí ‚Ñù) f (ùìù x) ‚Üî ‚àÉ hx : 0 ‚â§ x, Tendsto m f (ùìù ‚ü®x, hx‚ü©) :=
  ‚ü®fun h => ‚ü®ge_of_tendsto' h fun c => (m c).2, tendsto_coe.1 h‚ü©, fun ‚ü®_, hm‚ü© => tendsto_coe.2 hm‚ü©
#align nnreal.tendsto_coe' NNReal.tendsto_coe'

@[simp] theorem map_coe_atTop : map toReal atTop = atTop := map_val_Ici_atTop 0
#align nnreal.map_coe_at_top NNReal.map_coe_atTop

theorem comap_coe_atTop : comap toReal atTop = atTop := (atTop_Ici_eq 0).symm
#align nnreal.comap_coe_at_top NNReal.comap_coe_atTop

@[simp, norm_cast]
theorem tendsto_coe_atTop {f : Filter Œ±} {m : Œ± ‚Üí ‚Ñù‚â•0} :
    Tendsto (fun a => (m a : ‚Ñù)) f atTop ‚Üî Tendsto m f atTop :=
  tendsto_Ici_atTop.symm
#align nnreal.tendsto_coe_at_top NNReal.tendsto_coe_atTop

theorem _root_.tendsto_real_toNNReal {f : Filter Œ±} {m : Œ± ‚Üí ‚Ñù} {x : ‚Ñù} (h : Tendsto m f (ùìù x)) :
    Tendsto (fun a => Real.toNNReal (m a)) f (ùìù (Real.toNNReal x)) :=
  (continuous_real_toNNReal.tendsto _).comp h
#align tendsto_real_to_nnreal tendsto_real_toNNReal

theorem _root_.tendsto_real_toNNReal_atTop : Tendsto Real.toNNReal atTop atTop := by
  rw [‚Üê tendsto_coe_atTop]
  exact tendsto_atTop_mono Real.le_coe_toNNReal tendsto_id
#align tendsto_real_to_nnreal_at_top tendsto_real_toNNReal_atTop

theorem nhds_zero : ùìù (0 : ‚Ñù‚â•0) = ‚®Ö (a : ‚Ñù‚â•0) (_ : a ‚â† 0), ùìü (Iio a) :=
  nhds_bot_order.trans <| by simp only [bot_lt_iff_ne_bot]; rfl
#align nnreal.nhds_zero NNReal.nhds_zero

theorem nhds_zero_basis : (ùìù (0 : ‚Ñù‚â•0)).HasBasis (fun a : ‚Ñù‚â•0 => 0 < a) fun a => Iio a :=
  nhds_bot_basis
#align nnreal.nhds_zero_basis NNReal.nhds_zero_basis

instance : ContinuousSub ‚Ñù‚â•0 :=
  ‚ü®((continuous_coe.fst'.sub continuous_coe.snd').max continuous_const).subtype_mk _‚ü©

instance : HasContinuousInv‚ÇÄ ‚Ñù‚â•0 := inferInstance

instance [TopologicalSpace Œ±] [MulAction ‚Ñù Œ±] [ContinuousSMul ‚Ñù Œ±] :
    ContinuousSMul ‚Ñù‚â•0 Œ± where
  continuous_smul := continuous_induced_dom.fst'.smul continuous_snd

@[norm_cast]
theorem hasSum_coe {f : Œ± ‚Üí ‚Ñù‚â•0} {r : ‚Ñù‚â•0} : HasSum (fun a => (f a : ‚Ñù)) (r : ‚Ñù) ‚Üî HasSum f r := by
  simp only [HasSum, ‚Üê coe_sum, tendsto_coe]
#align nnreal.has_sum_coe NNReal.hasSum_coe

protected theorem _root_.HasSum.toNNReal {f : Œ± ‚Üí ‚Ñù} {y : ‚Ñù} (hf‚ÇÄ : ‚àÄ n, 0 ‚â§ f n)
    (hy : HasSum f y) : HasSum (fun x => Real.toNNReal (f x)) y.toNNReal := by
  lift y to ‚Ñù‚â•0 using hy.nonneg hf‚ÇÄ
  lift f to Œ± ‚Üí ‚Ñù‚â•0 using hf‚ÇÄ
  simpa [hasSum_coe] using hy

theorem hasSum_real_toNNReal_of_nonneg {f : Œ± ‚Üí ‚Ñù} (hf_nonneg : ‚àÄ n, 0 ‚â§ f n) (hf : Summable f) :
    HasSum (fun n => Real.toNNReal (f n)) (Real.toNNReal (‚àë' n, f n)) :=
  hf.hasSum.toNNReal hf_nonneg
#align nnreal.has_sum_real_to_nnreal_of_nonneg NNReal.hasSum_real_toNNReal_of_nonneg

@[norm_cast]
theorem summable_coe {f : Œ± ‚Üí ‚Ñù‚â•0} : (Summable fun a => (f a : ‚Ñù)) ‚Üî Summable f := by
  constructor
  ¬∑ exact fun ‚ü®a, ha‚ü© => ‚ü®‚ü®a, ha.nonneg fun x => (f x).2‚ü©, hasSum_coe.1 ha‚ü©
  ¬∑ exact fun ‚ü®a, ha‚ü© => ‚ü®a.1, hasSum_coe.2 ha‚ü©
#align nnreal.summable_coe NNReal.summable_coe

theorem summable_mk {f : Œ± ‚Üí ‚Ñù} (hf : ‚àÄ n, 0 ‚â§ f n) :
    (@Summable ‚Ñù‚â•0 _ _ _ fun n => ‚ü®f n, hf n‚ü©) ‚Üî Summable f :=
  Iff.symm <| summable_coe (f := fun x => ‚ü®f x, hf x‚ü©)
#align nnreal.summable_coe_of_nonneg NNReal.summable_mk

open scoped Classical

@[norm_cast]
theorem coe_tsum {f : Œ± ‚Üí ‚Ñù‚â•0} : ‚Üë(‚àë' a, f a) = ‚àë' a, (f a : ‚Ñù) :=
  if hf : Summable f then Eq.symm <| (hasSum_coe.2 <| hf.hasSum).tsum_eq
  else by simp [tsum_def, hf, mt summable_coe.1 hf]
#align nnreal.coe_tsum NNReal.coe_tsum

theorem coe_tsum_of_nonneg {f : Œ± ‚Üí ‚Ñù} (hf‚ÇÅ : ‚àÄ n, 0 ‚â§ f n) :
    (‚ü®‚àë' n, f n, tsum_nonneg hf‚ÇÅ‚ü© : ‚Ñù‚â•0) = (‚àë' n, ‚ü®f n, hf‚ÇÅ n‚ü© : ‚Ñù‚â•0) :=
  NNReal.eq <| Eq.symm <| coe_tsum (f := fun x => ‚ü®f x, hf‚ÇÅ x‚ü©)
#align nnreal.coe_tsum_of_nonneg NNReal.coe_tsum_of_nonneg

nonrec theorem tsum_mul_left (a : ‚Ñù‚â•0) (f : Œ± ‚Üí ‚Ñù‚â•0) : ‚àë' x, a * f x = a * ‚àë' x, f x :=
  NNReal.eq <| by simp only [coe_tsum, NNReal.coe_mul, tsum_mul_left]
#align nnreal.tsum_mul_left NNReal.tsum_mul_left

nonrec theorem tsum_mul_right (f : Œ± ‚Üí ‚Ñù‚â•0) (a : ‚Ñù‚â•0) : ‚àë' x, f x * a = (‚àë' x, f x) * a :=
  NNReal.eq <| by simp only [coe_tsum, NNReal.coe_mul, tsum_mul_right]
#align nnreal.tsum_mul_right NNReal.tsum_mul_right

theorem summable_comp_injective {Œ≤ : Type*} {f : Œ± ‚Üí ‚Ñù‚â•0} (hf : Summable f) {i : Œ≤ ‚Üí Œ±}
    (hi : Function.Injective i) : Summable (f ‚àò i) := by
  rw [‚Üê summable_coe] at hf ‚ä¢
  exact hf.comp_injective hi
#align nnreal.summable_comp_injective NNReal.summable_comp_injective

theorem summable_nat_add (f : ‚Ñï ‚Üí ‚Ñù‚â•0) (hf : Summable f) (k : ‚Ñï) : Summable fun i => f (i + k) :=
  summable_comp_injective hf <| add_left_injective k
#align nnreal.summable_nat_add NNReal.summable_nat_add

nonrec theorem summable_nat_add_iff {f : ‚Ñï ‚Üí ‚Ñù‚â•0} (k : ‚Ñï) :
    (Summable fun i => f (i + k)) ‚Üî Summable f := by
  rw [‚Üê summable_coe, ‚Üê summable_coe]
  exact @summable_nat_add_iff ‚Ñù _ _ _ (fun i => (f i : ‚Ñù)) k
#align nnreal.summable_nat_add_iff NNReal.summable_nat_add_iff

nonrec theorem hasSum_nat_add_iff {f : ‚Ñï ‚Üí ‚Ñù‚â•0} (k : ‚Ñï) {a : ‚Ñù‚â•0} :
    HasSum (fun n => f (n + k)) a ‚Üî HasSum f (a + ‚àë i ‚àà range k, f i) := by
  rw [‚Üê hasSum_coe, hasSum_nat_add_iff (f := fun n => toReal (f n)) k]; norm_cast
#align nnreal.has_sum_nat_add_iff NNReal.hasSum_nat_add_iff

theorem sum_add_tsum_nat_add {f : ‚Ñï ‚Üí ‚Ñù‚â•0} (k : ‚Ñï) (hf : Summable f) :
    ‚àë' i, f i = (‚àë i ‚àà range k, f i) + ‚àë' i, f (i + k) :=
  (sum_add_tsum_nat_add' <| (summable_nat_add_iff k).2 hf).symm
#align nnreal.sum_add_tsum_nat_add NNReal.sum_add_tsum_nat_add

theorem iInf_real_pos_eq_iInf_nnreal_pos [CompleteLattice Œ±] {f : ‚Ñù ‚Üí Œ±} :
    ‚®Ö (n : ‚Ñù) (_ : 0 < n), f n = ‚®Ö (n : ‚Ñù‚â•0) (_ : 0 < n), f n :=
  le_antisymm (iInf_mono' fun r => ‚ü®r, le_rfl‚ü©) (iInf‚ÇÇ_mono' fun r hr => ‚ü®‚ü®r, hr.le‚ü©, hr, le_rfl‚ü©)
#align nnreal.infi_real_pos_eq_infi_nnreal_pos NNReal.iInf_real_pos_eq_iInf_nnreal_pos

end coe

theorem tendsto_cofinite_zero_of_summable {Œ±} {f : Œ± ‚Üí ‚Ñù‚â•0} (hf : Summable f) :
    Tendsto f cofinite (ùìù 0) := by
  simp only [‚Üê summable_coe, ‚Üê tendsto_coe] at hf ‚ä¢
  exact hf.tendsto_cofinite_zero
#align nnreal.tendsto_cofinite_zero_of_summable NNReal.tendsto_cofinite_zero_of_summable

theorem tendsto_atTop_zero_of_summable {f : ‚Ñï ‚Üí ‚Ñù‚â•0} (hf : Summable f) : Tendsto f atTop (ùìù 0) := by
  rw [‚Üê Nat.cofinite_eq_atTop]
  exact tendsto_cofinite_zero_of_summable hf
#align nnreal.tendsto_at_top_zero_of_summable NNReal.tendsto_atTop_zero_of_summable

/-- The sum over the complement of a finset tends to `0` when the finset grows to cover the whole
space. This does not need a summability assumption, as otherwise all sums are zero. -/
nonrec theorem tendsto_tsum_compl_atTop_zero {Œ± : Type*} (f : Œ± ‚Üí ‚Ñù‚â•0) :
    Tendsto (fun s : Finset Œ± => ‚àë' b : { x // x ‚àâ s }, f b) atTop (ùìù 0) := by
  simp_rw [‚Üê tendsto_coe, coe_tsum, NNReal.coe_zero]
  exact tendsto_tsum_compl_atTop_zero fun a : Œ± => (f a : ‚Ñù)
#align nnreal.tendsto_tsum_compl_at_top_zero NNReal.tendsto_tsum_compl_atTop_zero

/-- `x ‚Ü¶ x ^ n` as an order isomorphism of `‚Ñù‚â•0`. -/
def powOrderIso (n : ‚Ñï) (hn : n ‚â† 0) : ‚Ñù‚â•0 ‚âÉo ‚Ñù‚â•0 :=
  StrictMono.orderIsoOfSurjective (fun x ‚Ü¶ x ^ n) (fun x y h =>
      pow_left_strictMonoOn hn (zero_le x) (zero_le y) h) <|
    (continuous_id.pow _).surjective (tendsto_pow_atTop hn) <| by
      simpa [OrderBot.atBot_eq, pos_iff_ne_zero]
#align nnreal.pow_order_iso NNReal.powOrderIso

section Monotone

/-- A monotone, bounded above sequence `f : ‚Ñï ‚Üí ‚Ñù` has a finite limit. -/
theorem _root_.Real.tendsto_of_bddAbove_monotone {f : ‚Ñï ‚Üí ‚Ñù} (h_bdd : BddAbove (Set.range f))
    (h_mon : Monotone f) : ‚àÉ r : ‚Ñù, Tendsto f atTop (ùìù r) := by
  obtain ‚ü®B, hB‚ü© := Real.exists_isLUB  (Set.range_nonempty f) h_bdd
  exact ‚ü®B, tendsto_atTop_isLUB h_mon hB‚ü©

/-- An antitone, bounded below sequence `f : ‚Ñï ‚Üí ‚Ñù` has a finite limit. -/
theorem _root_.Real.tendsto_of_bddBelow_antitone {f : ‚Ñï ‚Üí ‚Ñù} (h_bdd : BddBelow (Set.range f))
    (h_ant : Antitone f) : ‚àÉ r : ‚Ñù, Tendsto f atTop (ùìù r) := by
  obtain ‚ü®B, hB‚ü© := Real.exists_isGLB (Set.range_nonempty f) h_bdd
  exact ‚ü®B, tendsto_atTop_isGLB h_ant hB‚ü©

/-- An antitone sequence `f : ‚Ñï ‚Üí ‚Ñù‚â•0` has a finite limit. -/
theorem tendsto_of_antitone {f : ‚Ñï ‚Üí ‚Ñù‚â•0} (h_ant : Antitone f) :
    ‚àÉ r : ‚Ñù‚â•0, Tendsto f atTop (ùìù r) := by
  have h_bdd_0 : (0 : ‚Ñù) ‚àà lowerBounds (Set.range fun n : ‚Ñï => (f n : ‚Ñù)) := by
    rintro r ‚ü®n, hn‚ü©
    simp_rw [‚Üê hn]
    exact NNReal.coe_nonneg _
  obtain ‚ü®L, hL‚ü© := Real.tendsto_of_bddBelow_antitone ‚ü®0, h_bdd_0‚ü© h_ant
  have hL0 : 0 ‚â§ L :=
    haveI h_glb : IsGLB (Set.range fun n => (f n : ‚Ñù)) L := isGLB_of_tendsto_atTop h_ant hL
    (le_isGLB_iff h_glb).mpr h_bdd_0
  exact ‚ü®‚ü®L, hL0‚ü©, NNReal.tendsto_coe.mp hL‚ü©

end Monotone

instance instProperSpace : ProperSpace ‚Ñù‚â•0 where
  isCompact_closedBall x r := by
    have emb : ClosedEmbedding ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) := Isometry.closedEmbedding fun _ ‚Ü¶ congrFun rfl
    exact emb.isCompact_preimage (K := Metric.closedBall x r) (isCompact_closedBall _ _)

end NNReal
