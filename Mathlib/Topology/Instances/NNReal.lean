/-
Copyright (c) 2018 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin
-/
import Mathlib.Topology.Algebra.InfiniteSum.Order
import Mathlib.Topology.Algebra.InfiniteSum.Ring
import Mathlib.Topology.Instances.Real

#align_import topology.instances.nnreal from "leanprover-community/mathlib"@"32253a1a1071173b33dc7d6a218cf722c6feb514"

/-!
# Topology on `â„â‰¥0`

The natural topology on `â„â‰¥0` (the one induced from `â„`), and a basic API.

## Main definitions

Instances for the following typeclasses are defined:

* `TopologicalSpace â„â‰¥0`
* `TopologicalSemiring â„â‰¥0`
* `SecondCountableTopology â„â‰¥0`
* `OrderTopology â„â‰¥0`
* `ContinuousSub â„â‰¥0`
* `HasContinuousInvâ‚€ â„â‰¥0` (continuity of `xâ»Â¹` away from `0`)
* `ContinuousSMul â„â‰¥0 Î±` (whenever `Î±` has a continuous `MulAction â„ Î±`)

Everything is inherited from the corresponding structures on the reals.

## Main statements

Various mathematically trivial lemmas are proved about the compatibility
of limits and sums in `â„â‰¥0` and `â„`. For example

* `tendsto_coe {f : Filter Î±} {m : Î± â†’ â„â‰¥0} {x : â„â‰¥0} :
  Filter.Tendsto (fun a, (m a : â„)) f (ğ“ (x : â„)) â†” Filter.Tendsto m f (ğ“ x)`

says that the limit of a filter along a map to `â„â‰¥0` is the same in `â„` and `â„â‰¥0`, and

* `coe_tsum {f : Î± â†’ â„â‰¥0} : ((âˆ‘'a, f a) : â„) = (âˆ‘'a, (f a : â„))`

says that says that a sum of elements in `â„â‰¥0` is the same in `â„` and `â„â‰¥0`.

Similarly, some mathematically trivial lemmas about infinite sums are proved,
a few of which rely on the fact that subtraction is continuous.

-/


noncomputable section

open Set TopologicalSpace Metric Filter

open Topology

namespace NNReal

open NNReal BigOperators Filter

instance : TopologicalSpace â„â‰¥0 := inferInstance

-- short-circuit type class inference
instance : TopologicalSemiring â„â‰¥0 where
  toContinuousAdd := continuousAdd_induced toRealHom
  toContinuousMul := continuousMul_induced toRealHom

instance : SecondCountableTopology â„â‰¥0 :=
  inferInstanceAs (SecondCountableTopology { x : â„ | 0 â‰¤ x })

instance : OrderTopology â„â‰¥0 :=
  orderTopology_of_ordConnected (t := Ici 0)

instance : CompleteSpace â„â‰¥0 :=
  isClosed_Ici.completeSpace_coe

instance : ContinuousStar â„â‰¥0 where
  continuous_star := continuous_id
section coe

variable {Î± : Type*}

open Filter Finset

theorem _root_.continuous_real_toNNReal : Continuous Real.toNNReal :=
  (continuous_id.max continuous_const).subtype_mk _
#align continuous_real_to_nnreal continuous_real_toNNReal

theorem continuous_coe : Continuous ((â†‘) : â„â‰¥0 â†’ â„) :=
  continuous_subtype_val
#align nnreal.continuous_coe NNReal.continuous_coe

/-- Embedding of `â„â‰¥0` to `â„` as a bundled continuous map. -/
@[simps (config := .asFn)]
def _root_.ContinuousMap.coeNNRealReal : C(â„â‰¥0, â„) :=
  âŸ¨(â†‘), continuous_coeâŸ©
#align continuous_map.coe_nnreal_real ContinuousMap.coeNNRealReal
#align continuous_map.coe_nnreal_real_apply ContinuousMap.coeNNRealReal_apply

instance ContinuousMap.canLift {X : Type*} [TopologicalSpace X] :
    CanLift C(X, â„) C(X, â„â‰¥0) ContinuousMap.coeNNRealReal.comp fun f => âˆ€ x, 0 â‰¤ f x where
  prf f hf := âŸ¨âŸ¨fun x => âŸ¨f x, hf xâŸ©, f.2.subtype_mk _âŸ©, FunLike.ext' rflâŸ©
#align nnreal.continuous_map.can_lift NNReal.ContinuousMap.canLift

@[simp, norm_cast]
theorem tendsto_coe {f : Filter Î±} {m : Î± â†’ â„â‰¥0} {x : â„â‰¥0} :
    Tendsto (fun a => (m a : â„)) f (ğ“ (x : â„)) â†” Tendsto m f (ğ“ x) :=
  tendsto_subtype_rng.symm
#align nnreal.tendsto_coe NNReal.tendsto_coe

theorem tendsto_coe' {f : Filter Î±} [NeBot f] {m : Î± â†’ â„â‰¥0} {x : â„} :
    Tendsto (fun a => m a : Î± â†’ â„) f (ğ“ x) â†” âˆƒ hx : 0 â‰¤ x, Tendsto m f (ğ“ âŸ¨x, hxâŸ©) :=
  âŸ¨fun h => âŸ¨ge_of_tendsto' h fun c => (m c).2, tendsto_coe.1 hâŸ©, fun âŸ¨_, hmâŸ© => tendsto_coe.2 hmâŸ©
#align nnreal.tendsto_coe' NNReal.tendsto_coe'

@[simp] theorem map_coe_atTop : map toReal atTop = atTop := map_val_Ici_atTop 0
#align nnreal.map_coe_at_top NNReal.map_coe_atTop

theorem comap_coe_atTop : comap toReal atTop = atTop := (atTop_Ici_eq 0).symm
#align nnreal.comap_coe_at_top NNReal.comap_coe_atTop

@[simp, norm_cast]
theorem tendsto_coe_atTop {f : Filter Î±} {m : Î± â†’ â„â‰¥0} :
    Tendsto (fun a => (m a : â„)) f atTop â†” Tendsto m f atTop :=
  tendsto_Ici_atTop.symm
#align nnreal.tendsto_coe_at_top NNReal.tendsto_coe_atTop

theorem _root_.tendsto_real_toNNReal {f : Filter Î±} {m : Î± â†’ â„} {x : â„} (h : Tendsto m f (ğ“ x)) :
    Tendsto (fun a => Real.toNNReal (m a)) f (ğ“ (Real.toNNReal x)) :=
  (continuous_real_toNNReal.tendsto _).comp h
#align tendsto_real_to_nnreal tendsto_real_toNNReal

theorem _root_.tendsto_real_toNNReal_atTop : Tendsto Real.toNNReal atTop atTop := by
  rw [â† tendsto_coe_atTop]
  exact tendsto_atTop_mono Real.le_coe_toNNReal tendsto_id
#align tendsto_real_to_nnreal_at_top tendsto_real_toNNReal_atTop

theorem nhds_zero : ğ“ (0 : â„â‰¥0) = â¨… (a : â„â‰¥0) (_ : a â‰  0), ğ“Ÿ (Iio a) :=
  nhds_bot_order.trans <| by simp only [bot_lt_iff_ne_bot]; rfl
#align nnreal.nhds_zero NNReal.nhds_zero

theorem nhds_zero_basis : (ğ“ (0 : â„â‰¥0)).HasBasis (fun a : â„â‰¥0 => 0 < a) fun a => Iio a :=
  nhds_bot_basis
#align nnreal.nhds_zero_basis NNReal.nhds_zero_basis

instance : ContinuousSub â„â‰¥0 :=
  âŸ¨((continuous_coe.fst'.sub continuous_coe.snd').max continuous_const).subtype_mk _âŸ©

instance : HasContinuousInvâ‚€ â„â‰¥0 := inferInstance

instance [TopologicalSpace Î±] [MulAction â„ Î±] [ContinuousSMul â„ Î±] :
    ContinuousSMul â„â‰¥0 Î± where
  continuous_smul := continuous_induced_dom.fst'.smul continuous_snd

@[norm_cast]
theorem hasSum_coe {f : Î± â†’ â„â‰¥0} {r : â„â‰¥0} : HasSum (fun a => (f a : â„)) (r : â„) â†” HasSum f r := by
  simp only [HasSum, â† coe_sum, tendsto_coe]
#align nnreal.has_sum_coe NNReal.hasSum_coe

protected theorem _root_.HasSum.toNNReal {f : Î± â†’ â„} {y : â„} (hfâ‚€ : âˆ€ n, 0 â‰¤ f n)
    (hy : HasSum f y) : HasSum (fun x => Real.toNNReal (f x)) y.toNNReal := by
  lift y to â„â‰¥0 using hy.nonneg hfâ‚€
  lift f to Î± â†’ â„â‰¥0 using hfâ‚€
  simpa [hasSum_coe] using hy

theorem hasSum_real_toNNReal_of_nonneg {f : Î± â†’ â„} (hf_nonneg : âˆ€ n, 0 â‰¤ f n) (hf : Summable f) :
    HasSum (fun n => Real.toNNReal (f n)) (Real.toNNReal (âˆ‘' n, f n)) :=
  hf.hasSum.toNNReal hf_nonneg
#align nnreal.has_sum_real_to_nnreal_of_nonneg NNReal.hasSum_real_toNNReal_of_nonneg

@[norm_cast]
theorem summable_coe {f : Î± â†’ â„â‰¥0} : (Summable fun a => (f a : â„)) â†” Summable f := by
  constructor
  exact fun âŸ¨a, haâŸ© => âŸ¨âŸ¨a, ha.nonneg fun x => (f x).2âŸ©, hasSum_coe.1 haâŸ©
  exact fun âŸ¨a, haâŸ© => âŸ¨a.1, hasSum_coe.2 haâŸ©
#align nnreal.summable_coe NNReal.summable_coe

theorem summable_mk {f : Î± â†’ â„} (hf : âˆ€ n, 0 â‰¤ f n) :
    (@Summable â„â‰¥0 _ _ _ fun n => âŸ¨f n, hf nâŸ©) â†” Summable f :=
  Iff.symm <| summable_coe (f := fun x => âŸ¨f x, hf xâŸ©)
#align nnreal.summable_coe_of_nonneg NNReal.summable_mk

open Classical

@[norm_cast]
theorem coe_tsum {f : Î± â†’ â„â‰¥0} : â†‘(âˆ‘' a, f a) = âˆ‘' a, (f a : â„) :=
  if hf : Summable f then Eq.symm <| (hasSum_coe.2 <| hf.hasSum).tsum_eq
  else by simp [tsum_def, hf, mt summable_coe.1 hf]
#align nnreal.coe_tsum NNReal.coe_tsum

theorem coe_tsum_of_nonneg {f : Î± â†’ â„} (hfâ‚ : âˆ€ n, 0 â‰¤ f n) :
    (âŸ¨âˆ‘' n, f n, tsum_nonneg hfâ‚âŸ© : â„â‰¥0) = (âˆ‘' n, âŸ¨f n, hfâ‚ nâŸ© : â„â‰¥0) :=
  NNReal.eq <| Eq.symm <| coe_tsum (f := fun x => âŸ¨f x, hfâ‚ xâŸ©)
#align nnreal.coe_tsum_of_nonneg NNReal.coe_tsum_of_nonneg

nonrec theorem tsum_mul_left (a : â„â‰¥0) (f : Î± â†’ â„â‰¥0) : âˆ‘' x, a * f x = a * âˆ‘' x, f x :=
  NNReal.eq <| by simp only [coe_tsum, NNReal.coe_mul, tsum_mul_left]
#align nnreal.tsum_mul_left NNReal.tsum_mul_left

nonrec theorem tsum_mul_right (f : Î± â†’ â„â‰¥0) (a : â„â‰¥0) : âˆ‘' x, f x * a = (âˆ‘' x, f x) * a :=
  NNReal.eq <| by simp only [coe_tsum, NNReal.coe_mul, tsum_mul_right]
#align nnreal.tsum_mul_right NNReal.tsum_mul_right

theorem summable_comp_injective {Î² : Type*} {f : Î± â†’ â„â‰¥0} (hf : Summable f) {i : Î² â†’ Î±}
    (hi : Function.Injective i) : Summable (f âˆ˜ i) := by
  rw [â† summable_coe] at hf âŠ¢
  exact hf.comp_injective hi
#align nnreal.summable_comp_injective NNReal.summable_comp_injective

theorem summable_nat_add (f : â„• â†’ â„â‰¥0) (hf : Summable f) (k : â„•) : Summable fun i => f (i + k) :=
  summable_comp_injective hf <| add_left_injective k
#align nnreal.summable_nat_add NNReal.summable_nat_add

nonrec theorem summable_nat_add_iff {f : â„• â†’ â„â‰¥0} (k : â„•) :
    (Summable fun i => f (i + k)) â†” Summable f := by
  rw [â† summable_coe, â† summable_coe]
  exact @summable_nat_add_iff â„ _ _ _ (fun i => (f i : â„)) k
#align nnreal.summable_nat_add_iff NNReal.summable_nat_add_iff

nonrec theorem hasSum_nat_add_iff {f : â„• â†’ â„â‰¥0} (k : â„•) {a : â„â‰¥0} :
    HasSum (fun n => f (n + k)) a â†” HasSum f (a + âˆ‘ i in range k, f i) := by
  rw [â† hasSum_coe, hasSum_nat_add_iff (f := fun n => toReal (f n)) k]; norm_cast
#align nnreal.has_sum_nat_add_iff NNReal.hasSum_nat_add_iff

theorem sum_add_tsum_nat_add {f : â„• â†’ â„â‰¥0} (k : â„•) (hf : Summable f) :
    âˆ‘' i, f i = (âˆ‘ i in range k, f i) + âˆ‘' i, f (i + k) :=
  (sum_add_tsum_nat_add' <| (summable_nat_add_iff k).2 hf).symm
#align nnreal.sum_add_tsum_nat_add NNReal.sum_add_tsum_nat_add

theorem iInf_real_pos_eq_iInf_nnreal_pos [CompleteLattice Î±] {f : â„ â†’ Î±} :
    â¨… (n : â„) (_ : 0 < n), f n = â¨… (n : â„â‰¥0) (_ : 0 < n), f n :=
  le_antisymm (iInf_mono' fun r => âŸ¨r, le_rflâŸ©) (iInfâ‚‚_mono' fun r hr => âŸ¨âŸ¨r, hr.leâŸ©, hr, le_rflâŸ©)
#align nnreal.infi_real_pos_eq_infi_nnreal_pos NNReal.iInf_real_pos_eq_iInf_nnreal_pos

end coe

theorem tendsto_cofinite_zero_of_summable {Î±} {f : Î± â†’ â„â‰¥0} (hf : Summable f) :
    Tendsto f cofinite (ğ“ 0) := by
  simp only [â† summable_coe, â† tendsto_coe] at hf âŠ¢
  exact hf.tendsto_cofinite_zero
#align nnreal.tendsto_cofinite_zero_of_summable NNReal.tendsto_cofinite_zero_of_summable

theorem tendsto_atTop_zero_of_summable {f : â„• â†’ â„â‰¥0} (hf : Summable f) : Tendsto f atTop (ğ“ 0) := by
  rw [â† Nat.cofinite_eq_atTop]
  exact tendsto_cofinite_zero_of_summable hf
#align nnreal.tendsto_at_top_zero_of_summable NNReal.tendsto_atTop_zero_of_summable

/-- The sum over the complement of a finset tends to `0` when the finset grows to cover the whole
space. This does not need a summability assumption, as otherwise all sums are zero. -/
nonrec theorem tendsto_tsum_compl_atTop_zero {Î± : Type*} (f : Î± â†’ â„â‰¥0) :
    Tendsto (fun s : Finset Î± => âˆ‘' b : { x // x âˆ‰ s }, f b) atTop (ğ“ 0) := by
  simp_rw [â† tendsto_coe, coe_tsum, NNReal.coe_zero]
  exact tendsto_tsum_compl_atTop_zero fun a : Î± => (f a : â„)
#align nnreal.tendsto_tsum_compl_at_top_zero NNReal.tendsto_tsum_compl_atTop_zero

/-- `x â†¦ x ^ n` as an order isomorphism of `â„â‰¥0`. -/
def powOrderIso (n : â„•) (hn : n â‰  0) : â„â‰¥0 â‰ƒo â„â‰¥0 :=
  StrictMono.orderIsoOfSurjective (fun x â†¦ x ^ n) (fun x y h =>
      strictMonoOn_pow hn.bot_lt (zero_le x) (zero_le y) h) <|
    (continuous_id.pow _).surjective (tendsto_pow_atTop hn) <| by
      simpa [OrderBot.atBot_eq, pos_iff_ne_zero]
#align nnreal.pow_order_iso NNReal.powOrderIso

end NNReal
