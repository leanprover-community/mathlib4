/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Topology.Instances.Irrational
import Mathlib.Topology.Instances.Rat
import Mathlib.Topology.Compactification.OnePoint.Basic
import Mathlib.Topology.Metrizable.Uniformity

/-!
# Additional lemmas about the topology on rational numbers

The structure of a metric space on `‚Ñö` (`Rat.MetricSpace`) is introduced elsewhere, induced from
`‚Ñù`. In this file we prove some properties of this topological space and its one-point
compactification.

## Main statements

- `Rat.TotallyDisconnectedSpace`: `‚Ñö` is a totally disconnected space;

- `Rat.not_countably_generated_nhds_infty_opc`: the filter of neighbourhoods of infinity in
  `OnePoint ‚Ñö` is not countably generated.

## Notation

- `‚Ñö‚àû` is used as a local notation for `OnePoint ‚Ñö`
-/


open Set Metric Filter TopologicalSpace

open Topology OnePoint

local notation "‚Ñö‚àû" => OnePoint ‚Ñö

namespace Rat

variable {p : ‚Ñö} {s : Set ‚Ñö}

theorem interior_compact_eq_empty (hs : IsCompact s) : interior s = ‚àÖ :=
  isDenseEmbedding_coe_real.isDenseInducing.interior_compact_eq_empty dense_irrational hs

theorem dense_compl_compact (hs : IsCompact s) : Dense s·∂ú :=
  interior_eq_empty_iff_dense_compl.1 (interior_compact_eq_empty hs)

instance cocompact_inf_nhds_neBot : NeBot (cocompact ‚Ñö ‚äì ùìù p) := by
  refine (hasBasis_cocompact.inf (nhds_basis_opens _)).neBot_iff.2 ?_
  rintro ‚ü®s, o‚ü© ‚ü®hs, hpo, ho‚ü©; rw [inter_comm]
  exact (dense_compl_compact hs).inter_open_nonempty _ ho ‚ü®p, hpo‚ü©

theorem not_countably_generated_cocompact : ¬¨IsCountablyGenerated (cocompact ‚Ñö) := by
  intro H
  rcases exists_seq_tendsto (cocompact ‚Ñö ‚äì ùìù 0) with ‚ü®x, hx‚ü©
  rw [tendsto_inf] at hx; rcases hx with ‚ü®hxc, hx0‚ü©
  obtain ‚ü®n, hn‚ü© : ‚àÉ n : ‚Ñï, x n ‚àâ insert (0 : ‚Ñö) (range x) :=
    (hxc.eventually hx0.isCompact_insert_range.compl_mem_cocompact).exists
  exact hn (Or.inr ‚ü®n, rfl‚ü©)

theorem not_countably_generated_nhds_infty_opc : ¬¨IsCountablyGenerated (ùìù (‚àû : ‚Ñö‚àû)) := by
  intro
  have : IsCountablyGenerated (comap (OnePoint.some : ‚Ñö ‚Üí ‚Ñö‚àû) (ùìù ‚àû)) := by infer_instance
  rw [OnePoint.comap_coe_nhds_infty, coclosedCompact_eq_cocompact] at this
  exact not_countably_generated_cocompact this

theorem not_firstCountableTopology_opc : ¬¨FirstCountableTopology ‚Ñö‚àû := by
  intro
  exact not_countably_generated_nhds_infty_opc inferInstance

theorem not_secondCountableTopology_opc : ¬¨SecondCountableTopology ‚Ñö‚àû := by
  intro
  exact not_firstCountableTopology_opc inferInstance

instance : TotallyDisconnectedSpace ‚Ñö := by
  clear p s
  refine ‚ü®fun s hsu hs x hx y hy => ?_‚ü©; clear hsu
  by_contra! H : x ‚â† y
  wlog hlt : x < y
  ¬∑ apply this s hs y hy x hx H.symm <| H.lt_or_gt.resolve_left hlt
  rcases exists_irrational_btwn (Rat.cast_lt.2 hlt) with ‚ü®z, hz, hxz, hzy‚ü©
  have := hs.image _ continuous_coe_real.continuousOn
  rw [isPreconnected_iff_ordConnected] at this
  have : z ‚àà Rat.cast '' s :=
    this.out (mem_image_of_mem _ hx) (mem_image_of_mem _ hy) ‚ü®hxz.le, hzy.le‚ü©
  exact hz (image_subset_range _ _ this)

end Rat
