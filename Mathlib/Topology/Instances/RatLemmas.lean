/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Topology.Instances.Irrational
import Mathlib.Topology.Instances.Rat
import Mathlib.Topology.Compactification.OnePoint.Basic
import Mathlib.Topology.Metrizable.Uniformity

/-!
# Additional lemmas about the topology on rational numbers

The structure of a metric space on `â„š` (`Rat.MetricSpace`) is introduced elsewhere, induced from
`â„`. In this file we prove some properties of this topological space and its one-point
compactification.

## Main statements

- `Rat.TotallyDisconnectedSpace`: `â„š` is a totally disconnected space;

- `Rat.not_countably_generated_nhds_infty_opc`: the filter of neighbourhoods of infinity in
  `OnePoint â„š` is not countably generated.

## Notation

- `â„šâˆ` is used as a local notation for `OnePoint â„š`
-/


open Set Metric Filter TopologicalSpace

open Topology OnePoint

local notation "â„šâˆ" => OnePoint â„š

namespace Rat

variable {p : â„š} {s : Set â„š}

theorem interior_compact_eq_empty (hs : IsCompact s) : interior s = âˆ… :=
  isDenseEmbedding_coe_real.isDenseInducing.interior_compact_eq_empty dense_irrational hs

theorem dense_compl_compact (hs : IsCompact s) : Dense sá¶œ :=
  interior_eq_empty_iff_dense_compl.1 (interior_compact_eq_empty hs)

instance cocompact_inf_nhds_neBot : NeBot (cocompact â„š âŠ“ ğ“ p) := by
  refine (hasBasis_cocompact.inf (nhds_basis_opens _)).neBot_iff.2 ?_
  rintro âŸ¨s, oâŸ© âŸ¨hs, hpo, hoâŸ©; rw [inter_comm]
  exact (dense_compl_compact hs).inter_open_nonempty _ ho âŸ¨p, hpoâŸ©

theorem not_countably_generated_cocompact : Â¬IsCountablyGenerated (cocompact â„š) := by
  intro H
  rcases exists_seq_tendsto (cocompact â„š âŠ“ ğ“ 0) with âŸ¨x, hxâŸ©
  rw [tendsto_inf] at hx; rcases hx with âŸ¨hxc, hx0âŸ©
  obtain âŸ¨n, hnâŸ© : âˆƒ n : â„•, x n âˆ‰ insert (0 : â„š) (range x) :=
    (hxc.eventually hx0.isCompact_insert_range.compl_mem_cocompact).exists
  exact hn (Or.inr âŸ¨n, rflâŸ©)

theorem not_countably_generated_nhds_infty_opc : Â¬IsCountablyGenerated (ğ“ (âˆ : â„šâˆ)) := by
  intro
  have : IsCountablyGenerated (comap (OnePoint.some : â„š â†’ â„šâˆ) (ğ“ âˆ)) := by infer_instance
  rw [OnePoint.comap_coe_nhds_infty, coclosedCompact_eq_cocompact] at this
  exact not_countably_generated_cocompact this

theorem not_firstCountableTopology_opc : Â¬FirstCountableTopology â„šâˆ := by
  intro
  exact not_countably_generated_nhds_infty_opc inferInstance

theorem not_secondCountableTopology_opc : Â¬SecondCountableTopology â„šâˆ := by
  intro
  exact not_firstCountableTopology_opc inferInstance

instance : TotallyDisconnectedSpace â„š := by
  clear p s
  refine âŸ¨fun s hsu hs x hx y hy â†¦ ?_âŸ©; clear hsu
  by_contra! H : x â‰  y
  wlog hlt : x < y
  Â· apply this s hs y hy x hx H.symm <| H.lt_or_gt.resolve_left hlt
  rcases exists_irrational_btwn (Rat.cast_lt.2 hlt) with âŸ¨z, hz, hxz, hzyâŸ©
  have := hs.image _ continuous_coe_real.continuousOn
  rw [isPreconnected_iff_ordConnected] at this
  have : z âˆˆ Rat.cast '' s :=
    this.out (mem_image_of_mem _ hx) (mem_image_of_mem _ hy) âŸ¨hxz.le, hzy.leâŸ©
  exact hz (image_subset_range _ _ this)

end Rat
