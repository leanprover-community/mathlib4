/-
Copyright (c) 2021 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Data.EReal.Inv
import Mathlib.Topology.Semicontinuous

/-!
# Topological structure on `EReal`

We prove basic properties of the topology on `EReal`.

## Main results

* `Real.toEReal : ‚Ñù ‚Üí EReal` is an open embedding
* `ENNReal.toEReal : ‚Ñù‚â•0‚àû ‚Üí EReal` is a closed embedding
* The addition on `EReal` is continuous except at `(‚ä•, ‚ä§)` and at `(‚ä§, ‚ä•)`.
* Negation is a homeomorphism on `EReal`.

## Implementation

Most proofs are adapted from the corresponding proofs on `‚Ñù‚â•0‚àû`.
-/

noncomputable section

open Set Filter Metric TopologicalSpace Topology
open scoped ENNReal

variable {Œ± : Type*} [TopologicalSpace Œ±]

namespace EReal

/-! ### Real coercion -/

theorem isEmbedding_coe : IsEmbedding ((‚Üë) : ‚Ñù ‚Üí EReal) :=
  coe_strictMono.isEmbedding_of_ordConnected <| by rw [range_coe_eq_Ioo]; exact ordConnected_Ioo

theorem isOpenEmbedding_coe : IsOpenEmbedding ((‚Üë) : ‚Ñù ‚Üí EReal) :=
  ‚ü®isEmbedding_coe, by simp only [range_coe_eq_Ioo, isOpen_Ioo]‚ü©

@[norm_cast]
theorem tendsto_coe {Œ± : Type*} {f : Filter Œ±} {m : Œ± ‚Üí ‚Ñù} {a : ‚Ñù} :
    Tendsto (fun a => (m a : EReal)) f (ùìù ‚Üëa) ‚Üî Tendsto m f (ùìù a) :=
  isEmbedding_coe.tendsto_nhds_iff.symm

theorem _root_.continuous_coe_real_ereal : Continuous ((‚Üë) : ‚Ñù ‚Üí EReal) :=
  isEmbedding_coe.continuous

theorem continuous_coe_iff {f : Œ± ‚Üí ‚Ñù} : (Continuous fun a => (f a : EReal)) ‚Üî Continuous f :=
  isEmbedding_coe.continuous_iff.symm

theorem nhds_coe {r : ‚Ñù} : ùìù (r : EReal) = (ùìù r).map (‚Üë) :=
  (isOpenEmbedding_coe.map_nhds_eq r).symm

theorem nhds_coe_coe {r p : ‚Ñù} :
    ùìù ((r : EReal), (p : EReal)) = (ùìù (r, p)).map fun p : ‚Ñù √ó ‚Ñù => (‚Üëp.1, ‚Üëp.2) :=
  ((isOpenEmbedding_coe.prodMap isOpenEmbedding_coe).map_nhds_eq (r, p)).symm

theorem tendsto_toReal {a : EReal} (ha : a ‚â† ‚ä§) (h'a : a ‚â† ‚ä•) :
    Tendsto EReal.toReal (ùìù a) (ùìù a.toReal) := by
  lift a to ‚Ñù using ‚ü®ha, h'a‚ü©
  rw [nhds_coe, tendsto_map'_iff]
  exact tendsto_id

theorem continuousOn_toReal : ContinuousOn EReal.toReal ({‚ä•, ‚ä§}·∂ú : Set EReal) := fun _a ha =>
  ContinuousAt.continuousWithinAt (tendsto_toReal (mt Or.inr ha) (mt Or.inl ha))

/-- The set of finite `EReal` numbers is homeomorphic to `‚Ñù`. -/
def neBotTopHomeomorphReal : ({‚ä•, ‚ä§}·∂ú : Set EReal) ‚âÉ‚Çú ‚Ñù where
  toEquiv := neTopBotEquivReal
  continuous_toFun := continuousOn_iff_continuous_restrict.1 continuousOn_toReal
  continuous_invFun := continuous_coe_real_ereal.subtype_mk _

/-! ### ENNReal coercion -/

theorem isEmbedding_coe_ennreal : IsEmbedding ((‚Üë) : ‚Ñù‚â•0‚àû ‚Üí EReal) :=
  coe_ennreal_strictMono.isEmbedding_of_ordConnected <| by
    rw [range_coe_ennreal]; exact ordConnected_Ici

theorem isClosedEmbedding_coe_ennreal : IsClosedEmbedding ((‚Üë) : ‚Ñù‚â•0‚àû ‚Üí EReal) :=
  ‚ü®isEmbedding_coe_ennreal, by rw [range_coe_ennreal]; exact isClosed_Ici‚ü©

@[norm_cast]
theorem tendsto_coe_ennreal {Œ± : Type*} {f : Filter Œ±} {m : Œ± ‚Üí ‚Ñù‚â•0‚àû} {a : ‚Ñù‚â•0‚àû} :
    Tendsto (fun a => (m a : EReal)) f (ùìù ‚Üëa) ‚Üî Tendsto m f (ùìù a) :=
  isEmbedding_coe_ennreal.tendsto_nhds_iff.symm

theorem _root_.continuous_coe_ennreal_ereal : Continuous ((‚Üë) : ‚Ñù‚â•0‚àû ‚Üí EReal) :=
  isEmbedding_coe_ennreal.continuous

theorem continuous_coe_ennreal_iff {f : Œ± ‚Üí ‚Ñù‚â•0‚àû} :
    (Continuous fun a => (f a : EReal)) ‚Üî Continuous f :=
  isEmbedding_coe_ennreal.continuous_iff.symm

/-! ### Neighborhoods of infinity -/

theorem nhds_top : ùìù (‚ä§ : EReal) = ‚®Ö (a) (_ : a ‚â† ‚ä§), ùìü (Ioi a) :=
  nhds_top_order.trans <| by simp only [lt_top_iff_ne_top]

nonrec theorem nhds_top_basis : (ùìù (‚ä§ : EReal)).HasBasis (fun _ : ‚Ñù ‚Ü¶ True) (Ioi ¬∑) := by
  refine (nhds_top_basis (Œ± := EReal)).to_hasBasis (fun x hx => ?_)
    fun _ _ ‚Ü¶ ‚ü®_, coe_lt_top _, Subset.rfl‚ü©
  rcases exists_rat_btwn_of_lt hx with ‚ü®y, hxy, -‚ü©
  exact ‚ü®_, trivial, Ioi_subset_Ioi hxy.le‚ü©

theorem nhds_top' : ùìù (‚ä§ : EReal) = ‚®Ö a : ‚Ñù, ùìü (Ioi ‚Üëa) := nhds_top_basis.eq_iInf

theorem mem_nhds_top_iff {s : Set EReal} : s ‚àà ùìù (‚ä§ : EReal) ‚Üî ‚àÉ y : ‚Ñù, Ioi (y : EReal) ‚äÜ s :=
  nhds_top_basis.mem_iff.trans <| by simp only [true_and]

theorem tendsto_nhds_top_iff_real {Œ± : Type*} {m : Œ± ‚Üí EReal} {f : Filter Œ±} :
    Tendsto m f (ùìù ‚ä§) ‚Üî ‚àÄ x : ‚Ñù, ‚àÄ·∂† a in f, ‚Üëx < m a :=
  nhds_top_basis.tendsto_right_iff.trans <| by simp only [true_implies, mem_Ioi]

theorem nhds_bot : ùìù (‚ä• : EReal) = ‚®Ö (a) (_ : a ‚â† ‚ä•), ùìü (Iio a) :=
  nhds_bot_order.trans <| by simp only [bot_lt_iff_ne_bot]

theorem nhds_bot_basis : (ùìù (‚ä• : EReal)).HasBasis (fun _ : ‚Ñù ‚Ü¶ True) (Iio ¬∑) := by
  refine (_root_.nhds_bot_basis (Œ± := EReal)).to_hasBasis (fun x hx => ?_)
    fun _ _ ‚Ü¶ ‚ü®_, bot_lt_coe _, Subset.rfl‚ü©
  rcases exists_rat_btwn_of_lt hx with ‚ü®y, -, hxy‚ü©
  exact ‚ü®_, trivial, Iio_subset_Iio hxy.le‚ü©

theorem nhds_bot' : ùìù (‚ä• : EReal) = ‚®Ö a : ‚Ñù, ùìü (Iio ‚Üëa) :=
  nhds_bot_basis.eq_iInf

theorem mem_nhds_bot_iff {s : Set EReal} : s ‚àà ùìù (‚ä• : EReal) ‚Üî ‚àÉ y : ‚Ñù, Iio (y : EReal) ‚äÜ s :=
  nhds_bot_basis.mem_iff.trans <| by simp only [true_and]

theorem tendsto_nhds_bot_iff_real {Œ± : Type*} {m : Œ± ‚Üí EReal} {f : Filter Œ±} :
    Tendsto m f (ùìù ‚ä•) ‚Üî ‚àÄ x : ‚Ñù, ‚àÄ·∂† a in f, m a < x :=
  nhds_bot_basis.tendsto_right_iff.trans <| by simp only [true_implies, mem_Iio]

lemma nhdsWithin_top : ùìù[‚â†] (‚ä§ : EReal) = (atTop).map Real.toEReal := by
  apply (nhdsWithin_hasBasis nhds_top_basis_Ici _).ext (atTop_basis.map Real.toEReal)
  ¬∑ simp only [EReal.image_coe_Ici, true_and]
    intro x hx
    by_cases hx_bot : x = ‚ä•
    ¬∑ simp [hx_bot]
    lift x to ‚Ñù using ‚ü®hx.ne_top, hx_bot‚ü©
    refine ‚ü®x, fun x ‚ü®h1, h2‚ü© ‚Ü¶ ?_‚ü©
    simp [h1, h2.ne_top]
  ¬∑ simp only [EReal.image_coe_Ici, true_implies]
    refine fun x ‚Ü¶ ‚ü®x, ‚ü®EReal.coe_lt_top x, fun x ‚ü®(h1 : _ ‚â§ x), h2‚ü© ‚Ü¶ ?_‚ü©‚ü©
    simp [h1, Ne.lt_top' fun a ‚Ü¶ h2 a.symm]

lemma nhdsWithin_bot : ùìù[‚â†] (‚ä• : EReal) = (atBot).map Real.toEReal := by
  apply (nhdsWithin_hasBasis nhds_bot_basis_Iic _).ext (atBot_basis.map Real.toEReal)
  ¬∑ simp only [EReal.image_coe_Iic,
      true_and]
    intro x hx
    by_cases hx_top : x = ‚ä§
    ¬∑ simp [hx_top]
    lift x to ‚Ñù using ‚ü®hx_top, hx.ne_bot‚ü©
    refine ‚ü®x, fun x ‚ü®h1, h2‚ü© ‚Ü¶ ?_‚ü©
    simp [h2, h1.ne_bot]
  ¬∑ simp only [EReal.image_coe_Iic, true_implies]
    refine fun x ‚Ü¶ ‚ü®x, ‚ü®EReal.bot_lt_coe x, fun x ‚ü®(h1 : x ‚â§ _), h2‚ü© ‚Ü¶ ?_‚ü©‚ü©
    simp [h1, Ne.bot_lt' fun a ‚Ü¶ h2 a.symm]

omit [TopologicalSpace Œ±] in
@[simp]
lemma tendsto_coe_nhds_top_iff {f : Œ± ‚Üí ‚Ñù} {l : Filter Œ±} :
    Tendsto (fun x ‚Ü¶ Real.toEReal (f x)) l (ùìù ‚ä§) ‚Üî Tendsto f l atTop := by
  rw [tendsto_nhds_top_iff_real, atTop_basis_Ioi.tendsto_right_iff]; simp

lemma tendsto_coe_atTop : Tendsto Real.toEReal atTop (ùìù ‚ä§) :=
  tendsto_coe_nhds_top_iff.2 tendsto_id

omit [TopologicalSpace Œ±] in
@[simp]
lemma tendsto_coe_nhds_bot_iff {f : Œ± ‚Üí ‚Ñù} {l : Filter Œ±} :
    Tendsto (fun x ‚Ü¶ Real.toEReal (f x)) l (ùìù ‚ä•) ‚Üî Tendsto f l atBot := by
  rw [tendsto_nhds_bot_iff_real, atBot_basis_Iio.tendsto_right_iff]; simp

lemma tendsto_coe_atBot : Tendsto Real.toEReal atBot (ùìù ‚ä•) :=
  tendsto_coe_nhds_bot_iff.2 tendsto_id


lemma tendsto_toReal_atTop : Tendsto EReal.toReal (ùìù[‚â†] ‚ä§) atTop := by
  rw [nhdsWithin_top, tendsto_map'_iff]
  exact tendsto_id

lemma tendsto_toReal_atBot : Tendsto EReal.toReal (ùìù[‚â†] ‚ä•) atBot := by
  rw [nhdsWithin_bot, tendsto_map'_iff]
  exact tendsto_id

/-! ### toENNReal -/

lemma continuous_toENNReal : Continuous EReal.toENNReal := by
  refine continuous_iff_continuousAt.mpr fun x ‚Ü¶ ?_
  by_cases h_top : x = ‚ä§
  ¬∑ simp only [ContinuousAt, h_top, toENNReal_top]
    refine ENNReal.tendsto_nhds_top fun n ‚Ü¶ ?_
    filter_upwards [eventually_gt_nhds (coe_lt_top n)] with y hy
    exact toENNReal_coe (x := n) ‚ñ∏ toENNReal_lt_toENNReal (coe_ennreal_nonneg _) hy
  refine ContinuousOn.continuousAt ?_ (compl_singleton_mem_nhds_iff.mpr h_top)
  refine (continuousOn_of_forall_continuousAt fun x hx ‚Ü¶ ?_).congr (fun _ h ‚Ü¶ toENNReal_of_ne_top h)
  by_cases h_bot : x = ‚ä•
  ¬∑ refine tendsto_nhds_of_eventually_eq ?_
    rw [h_bot, nhds_bot_basis.eventually_iff]
    simp [toReal_bot, ENNReal.ofReal_zero, ENNReal.ofReal_eq_zero, true_and]
    exact ‚ü®0, fun _ hx ‚Ü¶ toReal_nonpos hx.le‚ü©
  refine ENNReal.continuous_ofReal.continuousAt.comp' <| continuousOn_toReal.continuousAt
    <| (toFinite _).isClosed.compl_mem_nhds ?_
  simp_all only [mem_compl_iff, mem_singleton_iff, mem_insert_iff, or_self, not_false_eq_true]

@[fun_prop]
lemma _root_.Continuous.ereal_toENNReal {Œ± : Type*} [TopologicalSpace Œ±] {f : Œ± ‚Üí EReal}
    (hf : Continuous f) :
    Continuous fun x => (f x).toENNReal :=
  continuous_toENNReal.comp hf

@[deprecated (since := "2025-03-05")] alias _root_.Continous.ereal_toENNReal :=
  _root_.Continuous.ereal_toENNReal

@[fun_prop]
lemma _root_.ContinuousOn.ereal_toENNReal {Œ± : Type*} [TopologicalSpace Œ±] {s : Set Œ±}
    {f : Œ± ‚Üí EReal} (hf : ContinuousOn f s) :
    ContinuousOn (fun x => (f x).toENNReal) s :=
  continuous_toENNReal.comp_continuousOn hf

@[fun_prop]
lemma _root_.ContinuousWithinAt.ereal_toENNReal {Œ± : Type*} [TopologicalSpace Œ±] {f : Œ± ‚Üí EReal}
    {s : Set Œ±} {x : Œ±} (hf : ContinuousWithinAt f s x) :
    ContinuousWithinAt (fun x => (f x).toENNReal) s x :=
  continuous_toENNReal.continuousAt.comp_continuousWithinAt hf

@[fun_prop]
lemma _root_.ContinuousAt.ereal_toENNReal {Œ± : Type*} [TopologicalSpace Œ±] {f : Œ± ‚Üí EReal}
    {x : Œ±} (hf : ContinuousAt f x) :
    ContinuousAt (fun x => (f x).toENNReal) x :=
  continuous_toENNReal.continuousAt.comp hf

/-! ### Infs and Sups -/

variable {Œ± : Type*} {u v : Œ± ‚Üí EReal}

lemma add_iInf_le_iInf_add : (‚®Ö x, u x) + ‚®Ö x, v x ‚â§ ‚®Ö x, (u + v) x :=
  le_iInf fun i ‚Ü¶ add_le_add (iInf_le u i) (iInf_le v i)

lemma iSup_add_le_add_iSup : ‚®Ü x, (u + v) x ‚â§ (‚®Ü x, u x) + ‚®Ü x, v x :=
  iSup_le fun i ‚Ü¶ add_le_add (le_iSup u i) (le_iSup v i)

/-! ### Liminfs and Limsups -/

section LimInfSup

variable {Œ± : Type*} {f : Filter Œ±} {u v : Œ± ‚Üí EReal}

lemma liminf_neg : liminf (- v) f = - limsup v f :=
  EReal.negOrderIso.limsup_apply.symm

lemma limsup_neg : limsup (- v) f = - liminf v f :=
  EReal.negOrderIso.liminf_apply.symm

lemma le_liminf_add : (liminf u f) + (liminf v f) ‚â§ liminf (u + v) f := by
  refine add_le_of_forall_lt fun a a_u b b_v ‚Ü¶ (le_liminf_iff).2 fun c c_ab ‚Ü¶ ?_
  filter_upwards [eventually_lt_of_lt_liminf a_u, eventually_lt_of_lt_liminf b_v] with x a_x b_x
  exact c_ab.trans (add_lt_add a_x b_x)

lemma limsup_add_le (h : limsup u f ‚â† ‚ä• ‚à® limsup v f ‚â† ‚ä§) (h' : limsup u f ‚â† ‚ä§ ‚à® limsup v f ‚â† ‚ä•) :
    limsup (u + v) f ‚â§ (limsup u f) + (limsup v f) := by
  refine le_add_of_forall_gt h h' fun a a_u b b_v ‚Ü¶ (limsup_le_iff).2 fun c c_ab ‚Ü¶ ?_
  filter_upwards [eventually_lt_of_limsup_lt a_u, eventually_lt_of_limsup_lt b_v] with x a_x b_x
  exact (add_lt_add a_x b_x).trans c_ab

lemma le_limsup_add : (limsup u f) + (liminf v f) ‚â§ limsup (u + v) f :=
  add_le_of_forall_lt fun _ a_u _ b_v ‚Ü¶ (le_limsup_iff).2 fun _ c_ab ‚Ü¶
    (((frequently_lt_of_lt_limsup) a_u).and_eventually ((eventually_lt_of_lt_liminf) b_v)).mono
    fun _ ab_x ‚Ü¶ c_ab.trans (add_lt_add ab_x.1 ab_x.2)

lemma liminf_add_le (h : limsup u f ‚â† ‚ä• ‚à® liminf v f ‚â† ‚ä§) (h' : limsup u f ‚â† ‚ä§ ‚à® liminf v f ‚â† ‚ä•) :
    liminf (u + v) f ‚â§ (limsup u f) + (liminf v f) :=
  le_add_of_forall_gt h h' fun _ a_u _ b_v ‚Ü¶ (liminf_le_iff).2 fun _ c_ab ‚Ü¶
    (((frequently_lt_of_liminf_lt) b_v).and_eventually ((eventually_lt_of_limsup_lt) a_u)).mono
    fun _ ab_x ‚Ü¶ (add_lt_add ab_x.2 ab_x.1).trans c_ab

lemma limsup_add_bot_of_ne_top (h : limsup u f = ‚ä•) (h' : limsup v f ‚â† ‚ä§) :
    limsup (u + v) f = ‚ä• := by
  apply le_bot_iff.1 ((limsup_add_le (.inr h') _).trans _)
  ¬∑ rw [h]; exact .inl bot_ne_top
  ¬∑ rw [h, bot_add]

lemma limsup_add_le_of_le {a b : EReal} (ha : limsup u f < a) (hb : limsup v f ‚â§ b) :
    limsup (u + v) f ‚â§ a + b := by
  rcases eq_top_or_lt_top b with rfl | h
  ¬∑ rw [add_top_of_ne_bot ha.ne_bot]; exact le_top
  ¬∑ exact (limsup_add_le (.inr (hb.trans_lt h).ne) (.inl ha.ne_top)).trans (add_le_add ha.le hb)

lemma liminf_add_gt_of_gt {a b : EReal} (ha : a < liminf u f) (hb : b < liminf v f) :
    a + b < liminf (u + v) f :=
  (add_lt_add ha hb).trans_le le_liminf_add

lemma liminf_add_top_of_ne_bot (h : liminf u f = ‚ä§) (h' : liminf v f ‚â† ‚ä•) :
    liminf (u + v) f = ‚ä§ := by
  apply top_le_iff.1 (le_trans _ le_liminf_add)
  rw [h, top_add_of_ne_bot h']

lemma le_limsup_mul (hu : ‚àÉ·∂† x in f, 0 ‚â§ u x) (hv : 0 ‚â§·∂†[f] v) :
    limsup u f * liminf v f ‚â§ limsup (u * v) f := by
  rcases f.eq_or_neBot with rfl | _
  ¬∑ rw [limsup_bot, limsup_bot, liminf_bot, bot_mul_top]
  have u0 : 0 ‚â§ limsup u f := le_limsup_of_frequently_le hu
  have uv0 : 0 ‚â§ limsup (u * v) f :=
    le_limsup_of_frequently_le <| (hu.and_eventually hv).mono fun _ ‚ü®hu, hv‚ü© ‚Ü¶ mul_nonneg hu hv
  refine mul_le_of_forall_lt_of_nonneg u0 uv0 fun a ha b hb ‚Ü¶ (le_limsup_iff).2 fun c c_ab ‚Ü¶ ?_
  refine (((frequently_lt_of_lt_limsup) (mem_Ioo.1 ha).2).and_eventually
    <| (eventually_lt_of_lt_liminf (mem_Ioo.1 hb).2).and
    <| hv).mono fun x ‚ü®xa, ‚ü®xb, vx‚ü©‚ü© ‚Ü¶ ?_
  exact c_ab.trans_le (mul_le_mul xa.le xb.le (mem_Ioo.1 hb).1.le ((mem_Ioo.1 ha).1.le.trans xa.le))

lemma limsup_mul_le (hu : ‚àÉ·∂† x in f, 0 ‚â§ u x) (hv : 0 ‚â§·∂†[f] v)
    (h‚ÇÅ : limsup u f ‚â† 0 ‚à® limsup v f ‚â† ‚ä§) (h‚ÇÇ : limsup u f ‚â† ‚ä§ ‚à® limsup v f ‚â† 0) :
    limsup (u * v) f ‚â§ limsup u f * limsup v f := by
  rcases f.eq_or_neBot with rfl | _
  ¬∑ rw [limsup_bot]; exact bot_le
  have u_0 : 0 ‚â§ limsup u f := le_limsup_of_frequently_le hu
  replace h‚ÇÅ : 0 < limsup u f ‚à® limsup v f ‚â† ‚ä§ := h‚ÇÅ.imp_left fun h ‚Ü¶ lt_of_le_of_ne u_0 h.symm
  replace h‚ÇÇ : limsup u f ‚â† ‚ä§ ‚à® 0 < limsup v f :=
    h‚ÇÇ.imp_right fun h ‚Ü¶ lt_of_le_of_ne (le_limsup_of_frequently_le hv.frequently) h.symm
  refine le_mul_of_forall_lt h‚ÇÅ h‚ÇÇ fun a a_u b b_v ‚Ü¶ (limsup_le_iff).2 fun c c_ab ‚Ü¶ ?_
  filter_upwards [eventually_lt_of_limsup_lt a_u, eventually_lt_of_limsup_lt b_v, hv]
    with x x_a x_b v_0
  apply lt_of_le_of_lt _ c_ab
  rcases lt_or_ge (u x) 0 with hux | hux
  ¬∑ apply (mul_nonpos_iff.2 (.inr ‚ü®hux.le, v_0‚ü©)).trans
    exact mul_nonneg (u_0.trans a_u.le) (v_0.trans x_b.le)
  ¬∑ exact mul_le_mul x_a.le x_b.le v_0 (hux.trans x_a.le)

lemma le_liminf_mul (hu : 0 ‚â§·∂†[f] u) (hv : 0 ‚â§·∂†[f] v) :
    liminf u f * liminf v f ‚â§ liminf (u * v) f := by
  apply mul_le_of_forall_lt_of_nonneg ((le_liminf_of_le) hu)
    <| (le_liminf_of_le) ((hu.and hv).mono fun x ‚ü®u0, v0‚ü© ‚Ü¶ mul_nonneg u0 v0)
  refine fun a ha b hb ‚Ü¶ (le_liminf_iff).2 fun c c_ab ‚Ü¶ ?_
  filter_upwards [eventually_lt_of_lt_liminf (mem_Ioo.1 ha).2,
    eventually_lt_of_lt_liminf (mem_Ioo.1 hb).2] with x xa xb
  exact c_ab.trans_le (mul_le_mul xa.le xb.le (mem_Ioo.1 hb).1.le ((mem_Ioo.1 ha).1.le.trans xa.le))

lemma liminf_mul_le [NeBot f] (hu : 0 ‚â§·∂†[f] u) (hv : 0 ‚â§·∂†[f] v)
    (h‚ÇÅ : limsup u f ‚â† 0 ‚à® liminf v f ‚â† ‚ä§) (h‚ÇÇ : limsup u f ‚â† ‚ä§ ‚à® liminf v f ‚â† 0) :
    liminf (u * v) f ‚â§ limsup u f * liminf v f := by
  replace h‚ÇÅ : 0 < limsup u f ‚à® liminf v f ‚â† ‚ä§ := by
    refine h‚ÇÅ.imp_left fun h ‚Ü¶ lt_of_le_of_ne ?_ h.symm
    exact le_of_eq_of_le (limsup_const 0).symm (limsup_le_limsup hu)
  replace h‚ÇÇ : limsup u f ‚â† ‚ä§ ‚à® 0 < liminf v f := by
    refine h‚ÇÇ.imp_right fun h ‚Ü¶ lt_of_le_of_ne ?_ h.symm
    exact le_of_eq_of_le (liminf_const 0).symm (liminf_le_liminf hv)
  refine le_mul_of_forall_lt h‚ÇÅ h‚ÇÇ fun a a_u b b_v ‚Ü¶ (liminf_le_iff).2 fun c c_ab ‚Ü¶ ?_
  refine (((frequently_lt_of_liminf_lt) b_v).and_eventually <| (eventually_lt_of_limsup_lt a_u).and
    <| hu.and hv).mono fun x ‚ü®x_v, x_u, u_0, v_0‚ü© ‚Ü¶ ?_
  exact (mul_le_mul x_u.le x_v.le v_0 (u_0.trans x_u.le)).trans_lt c_ab

end LimInfSup

/-! ### Continuity of addition -/

theorem continuousAt_add_coe_coe (a b : ‚Ñù) :
    ContinuousAt (fun p : EReal √ó EReal => p.1 + p.2) (a, b) := by
  simp only [ContinuousAt, nhds_coe_coe, ‚Üê coe_add, tendsto_map'_iff, Function.comp_def,
    tendsto_coe, tendsto_add]

theorem continuousAt_add_top_coe (a : ‚Ñù) :
    ContinuousAt (fun p : EReal √ó EReal => p.1 + p.2) (‚ä§, a) := by
  simp only [ContinuousAt, tendsto_nhds_top_iff_real, top_add_coe]
  refine fun r ‚Ü¶ ((lt_mem_nhds (coe_lt_top (r - (a - 1)))).prod_nhds
    (lt_mem_nhds <| EReal.coe_lt_coe_iff.2 <| sub_one_lt _)).mono fun _ h ‚Ü¶ ?_
  simpa only [‚Üê coe_add, _root_.sub_add_cancel] using add_lt_add h.1 h.2

theorem continuousAt_add_coe_top (a : ‚Ñù) :
    ContinuousAt (fun p : EReal √ó EReal => p.1 + p.2) (a, ‚ä§) := by
  simpa only [add_comm, Function.comp_def, ContinuousAt, Prod.swap]
    using Tendsto.comp (continuousAt_add_top_coe a) (continuous_swap.tendsto ((a : EReal), ‚ä§))

theorem continuousAt_add_top_top : ContinuousAt (fun p : EReal √ó EReal => p.1 + p.2) (‚ä§, ‚ä§) := by
  simp only [ContinuousAt, tendsto_nhds_top_iff_real, top_add_top]
  refine fun r ‚Ü¶ ((lt_mem_nhds (coe_lt_top 0)).prod_nhds
    (lt_mem_nhds <| coe_lt_top r)).mono fun _ h ‚Ü¶ ?_
  simpa only [coe_zero, zero_add] using add_lt_add h.1 h.2

theorem continuousAt_add_bot_coe (a : ‚Ñù) :
    ContinuousAt (fun p : EReal √ó EReal => p.1 + p.2) (‚ä•, a) := by
  simp only [ContinuousAt, tendsto_nhds_bot_iff_real, bot_add]
  refine fun r ‚Ü¶ ((gt_mem_nhds (bot_lt_coe (r - (a + 1)))).prod_nhds
    (gt_mem_nhds <| EReal.coe_lt_coe_iff.2 <| lt_add_one _)).mono fun _ h ‚Ü¶ ?_
  simpa only [‚Üê coe_add, _root_.sub_add_cancel] using add_lt_add h.1 h.2

theorem continuousAt_add_coe_bot (a : ‚Ñù) :
    ContinuousAt (fun p : EReal √ó EReal => p.1 + p.2) (a, ‚ä•) := by
  simpa only [add_comm, Function.comp_def, ContinuousAt, Prod.swap]
    using Tendsto.comp (continuousAt_add_bot_coe a) (continuous_swap.tendsto ((a : EReal), ‚ä•))

theorem continuousAt_add_bot_bot : ContinuousAt (fun p : EReal √ó EReal => p.1 + p.2) (‚ä•, ‚ä•) := by
  simp only [ContinuousAt, tendsto_nhds_bot_iff_real, bot_add]
  refine fun r ‚Ü¶ ((gt_mem_nhds (bot_lt_coe 0)).prod_nhds
    (gt_mem_nhds <| bot_lt_coe r)).mono fun _ h ‚Ü¶ ?_
  simpa only [coe_zero, zero_add] using add_lt_add h.1 h.2

/-- The addition on `EReal` is continuous except where it doesn't make sense (i.e., at `(‚ä•, ‚ä§)`
and at `(‚ä§, ‚ä•)`). -/
theorem continuousAt_add {p : EReal √ó EReal} (h : p.1 ‚â† ‚ä§ ‚à® p.2 ‚â† ‚ä•) (h' : p.1 ‚â† ‚ä• ‚à® p.2 ‚â† ‚ä§) :
    ContinuousAt (fun p : EReal √ó EReal => p.1 + p.2) p := by
  rcases p with ‚ü®x, y‚ü©
  induction x <;> induction y
  ¬∑ exact continuousAt_add_bot_bot
  ¬∑ exact continuousAt_add_bot_coe _
  ¬∑ simp at h'
  ¬∑ exact continuousAt_add_coe_bot _
  ¬∑ exact continuousAt_add_coe_coe _ _
  ¬∑ exact continuousAt_add_coe_top _
  ¬∑ simp at h
  ¬∑ exact continuousAt_add_top_coe _
  ¬∑ exact continuousAt_add_top_top

lemma lowerSemicontinuous_add : LowerSemicontinuous fun p : EReal √ó EReal ‚Ü¶ p.1 + p.2 := by
  intro x y
  by_cases hx‚ÇÅ : x.1 = ‚ä•
  ¬∑ simp [hx‚ÇÅ]
  by_cases hx‚ÇÇ : x.2 = ‚ä•
  ¬∑ simp [hx‚ÇÇ]
  ¬∑ exact continuousAt_add (.inr hx‚ÇÇ) (.inl hx‚ÇÅ) |>.lowerSemicontinuousAt _

/-! ### Continuity of multiplication -/

/- Outside of indeterminacies `(0, ¬±‚àû)` and `(¬±‚àû, 0)`, the multiplication on `EReal` is continuous.
There are many different cases to consider, so we first prove some special cases and leverage as
much as possible the symmetries of the multiplication. -/

private lemma continuousAt_mul_swap {a b : EReal}
    (h : ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) (a, b)) :
    ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) (b, a) := by
  convert h.comp continuous_swap.continuousAt (x := (b, a))
  simp [mul_comm]

private lemma continuousAt_mul_symm1 {a b : EReal}
    (h : ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) (a, b)) :
    ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) (-a, b) := by
  have : (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) = (fun x : EReal ‚Ü¶ -x)
      ‚àò (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) ‚àò (fun p : EReal √ó EReal ‚Ü¶ (-p.1, p.2)) := by
    ext p
    simp
  rw [this]
  apply ContinuousAt.comp (Continuous.continuousAt continuous_neg)
    <| ContinuousAt.comp _ (ContinuousAt.prodMap (Continuous.continuousAt continuous_neg)
      (Continuous.continuousAt continuous_id))
  simp [h]

private lemma continuousAt_mul_symm2 {a b : EReal}
    (h : ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) (a, b)) :
    ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) (a, -b) :=
  continuousAt_mul_swap (continuousAt_mul_symm1 (continuousAt_mul_swap h))

private lemma continuousAt_mul_symm3 {a b : EReal}
    (h : ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) (a, b)) :
    ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) (-a, -b) :=
  continuousAt_mul_symm1 (continuousAt_mul_symm2 h)

private lemma continuousAt_mul_coe_coe (a b : ‚Ñù) :
    ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) (a, b) := by
  simp [ContinuousAt, EReal.nhds_coe_coe, ‚Üê EReal.coe_mul, Filter.tendsto_map'_iff,
    Function.comp_def, EReal.tendsto_coe, tendsto_mul]

private lemma continuousAt_mul_top_top :
    ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) (‚ä§, ‚ä§) := by
  simp only [ContinuousAt, EReal.top_mul_top, EReal.tendsto_nhds_top_iff_real]
  intro x
  rw [_root_.eventually_nhds_iff]
  use (Set.Ioi ((max x 0) : EReal)) √óÀ¢ (Set.Ioi 1)
  split_ands
  ¬∑ intro p p_in_prod
    simp only [Set.mem_prod, Set.mem_Ioi, max_lt_iff] at p_in_prod
    rcases p_in_prod with ‚ü®‚ü®p1_gt_x, p1_pos‚ü©, p2_gt_1‚ü©
    have := mul_le_mul_of_nonneg_left (le_of_lt p2_gt_1) (le_of_lt p1_pos)
    rw [mul_one p.1] at this
    exact lt_of_lt_of_le p1_gt_x this
  ¬∑ exact IsOpen.prod isOpen_Ioi isOpen_Ioi
  ¬∑ simp
  ¬∑ rw [Set.mem_Ioi, ‚Üê EReal.coe_one]; exact EReal.coe_lt_top 1

private lemma continuousAt_mul_top_pos {a : ‚Ñù} (h : 0 < a) :
    ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) (‚ä§, a) := by
  simp only [ContinuousAt, EReal.top_mul_coe_of_pos h, EReal.tendsto_nhds_top_iff_real]
  intro x
  rw [_root_.eventually_nhds_iff]
  use (Set.Ioi ((2*(max (x+1) 0)/a : ‚Ñù) : EReal)) √óÀ¢ (Set.Ioi ((a/2 : ‚Ñù) : EReal))
  split_ands
  ¬∑ intro p p_in_prod
    simp only [Set.mem_prod, Set.mem_Ioi] at p_in_prod
    rcases p_in_prod with ‚ü®p1_gt, p2_gt‚ü©
    have p1_pos : 0 < p.1 := by
      apply lt_of_le_of_lt _ p1_gt
      rw [EReal.coe_nonneg]
      apply mul_nonneg _ (le_of_lt (inv_pos_of_pos h))
      simp only [Nat.ofNat_pos, mul_nonneg_iff_of_pos_left, le_max_iff, le_refl, or_true]
    have a2_pos : 0 < ((a/2 : ‚Ñù) : EReal) := by rw [EReal.coe_pos]; linarith
    have lock := mul_le_mul_of_nonneg_right (le_of_lt p1_gt) (le_of_lt a2_pos)
    have key := mul_le_mul_of_nonneg_left (le_of_lt p2_gt) (le_of_lt p1_pos)
    replace lock := le_trans lock key
    apply lt_of_lt_of_le _ lock
    rw [‚Üê EReal.coe_mul, EReal.coe_lt_coe_iff, _root_.div_mul_div_comm, mul_comm,
      ‚Üê _root_.div_mul_div_comm, mul_div_right_comm]
    simp only [ne_eq, Ne.symm (ne_of_lt h), not_false_eq_true, _root_.div_self, OfNat.ofNat_ne_zero,
      one_mul, lt_max_iff, lt_add_iff_pos_right, zero_lt_one, true_or]
  ¬∑ exact IsOpen.prod isOpen_Ioi isOpen_Ioi
  ¬∑ simp
  ¬∑ simp [h]

private lemma continuousAt_mul_top_ne_zero {a : ‚Ñù} (h : a ‚â† 0) :
    ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) (‚ä§, a) := by
  rcases lt_or_gt_of_ne h with a_neg | a_pos
  ¬∑ exact neg_neg a ‚ñ∏ continuousAt_mul_symm2 (continuousAt_mul_top_pos (neg_pos.2 a_neg))
  ¬∑ exact continuousAt_mul_top_pos a_pos

/-- The multiplication on `EReal` is continuous except at indeterminacies
(i.e. whenever one value is zero and the other infinite). -/
theorem continuousAt_mul {p : EReal √ó EReal} (h‚ÇÅ : p.1 ‚â† 0 ‚à® p.2 ‚â† ‚ä•)
    (h‚ÇÇ : p.1 ‚â† 0 ‚à® p.2 ‚â† ‚ä§) (h‚ÇÉ : p.1 ‚â† ‚ä• ‚à® p.2 ‚â† 0) (h‚ÇÑ : p.1 ‚â† ‚ä§ ‚à® p.2 ‚â† 0) :
    ContinuousAt (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) p := by
  rcases p with ‚ü®x, y‚ü©
  induction x <;> induction y
  ¬∑ exact continuousAt_mul_symm3 continuousAt_mul_top_top
  ¬∑ simp only [ne_eq, not_true_eq_false, EReal.coe_eq_zero, false_or] at h‚ÇÉ
    exact continuousAt_mul_symm1 (continuousAt_mul_top_ne_zero h‚ÇÉ)
  ¬∑ exact EReal.neg_top ‚ñ∏ continuousAt_mul_symm1 continuousAt_mul_top_top
  ¬∑ simp only [ne_eq, EReal.coe_eq_zero, not_true_eq_false, or_false] at h‚ÇÅ
    exact continuousAt_mul_symm2 (continuousAt_mul_swap (continuousAt_mul_top_ne_zero h‚ÇÅ))
  ¬∑ exact continuousAt_mul_coe_coe _ _
  ¬∑ simp only [ne_eq, EReal.coe_eq_zero, not_true_eq_false, or_false] at h‚ÇÇ
    exact continuousAt_mul_swap (continuousAt_mul_top_ne_zero h‚ÇÇ)
  ¬∑ exact continuousAt_mul_symm2 continuousAt_mul_top_top
  ¬∑ simp only [ne_eq, not_true_eq_false, EReal.coe_eq_zero, false_or] at h‚ÇÑ
    exact continuousAt_mul_top_ne_zero h‚ÇÑ
  ¬∑ exact continuousAt_mul_top_top

variable {a b : EReal}

protected theorem tendsto_mul (h‚ÇÅ : a ‚â† 0 ‚à® b ‚â† ‚ä•) (h‚ÇÇ : a ‚â† 0 ‚à® b ‚â† ‚ä§) (h‚ÇÉ : a ‚â† ‚ä• ‚à® b ‚â† 0)
    (h‚ÇÑ : a ‚â† ‚ä§ ‚à® b ‚â† 0) :
    Tendsto (fun p : EReal √ó EReal ‚Ü¶ p.1 * p.2) (ùìù (a, b)) (ùìù (a * b)) :=
  (continuousAt_mul h‚ÇÅ h‚ÇÇ h‚ÇÉ h‚ÇÑ).tendsto

protected theorem Tendsto.mul {f : Filter Œ±} {ma : Œ± ‚Üí EReal} {mb : Œ± ‚Üí EReal} {a b : EReal}
    (hma : Tendsto ma f (ùìù a)) (hmb : Tendsto mb f (ùìù b)) (h‚ÇÅ : a ‚â† 0 ‚à® b ‚â† ‚ä•)
    (h‚ÇÇ : a ‚â† 0 ‚à® b ‚â† ‚ä§) (h‚ÇÉ : a ‚â† ‚ä• ‚à® b ‚â† 0) (h‚ÇÑ : a ‚â† ‚ä§ ‚à® b ‚â† 0) :
    Tendsto (fun x ‚Ü¶ ma x * mb x) f (ùìù (a * b)) :=
  (EReal.tendsto_mul h‚ÇÅ h‚ÇÇ h‚ÇÉ h‚ÇÑ).comp (hma.prodMk_nhds hmb)

protected theorem Tendsto.const_mul {f : Filter Œ±} {m : Œ± ‚Üí EReal} {a b : EReal}
    (hm : Tendsto m f (ùìù b)) (h‚ÇÅ : a ‚â† ‚ä• ‚à® b ‚â† 0) (h‚ÇÇ : a ‚â† ‚ä§ ‚à® b ‚â† 0) :
    Tendsto (fun b ‚Ü¶ a * m b) f (ùìù (a * b)) :=
  by_cases (fun (this : a = 0) => by simp [this, tendsto_const_nhds])
    fun ha : a ‚â† 0 => EReal.Tendsto.mul tendsto_const_nhds hm (Or.inl ha) (Or.inl ha) h‚ÇÅ h‚ÇÇ

protected theorem Tendsto.mul_const {f : Filter Œ±} {m : Œ± ‚Üí EReal} {a b : EReal}
    (hm : Tendsto m f (ùìù a)) (h‚ÇÅ : a ‚â† 0 ‚à® b ‚â† ‚ä•) (h‚ÇÇ : a ‚â† 0 ‚à® b ‚â† ‚ä§) :
    Tendsto (fun x ‚Ü¶ m x * b) f (ùìù (a * b)) := by
  simpa only [mul_comm] using EReal.Tendsto.const_mul hm h‚ÇÅ.symm h‚ÇÇ.symm

end EReal
