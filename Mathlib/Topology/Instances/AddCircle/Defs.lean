/-
Copyright (c) 2022 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
module

public import Mathlib.Algebra.Order.ToIntervalMod
public import Mathlib.Algebra.Ring.AddAut
public import Mathlib.Data.Nat.Totient
public import Mathlib.GroupTheory.Divisible
public import Mathlib.Topology.Algebra.IsUniformGroup.Basic
public import Mathlib.Topology.Algebra.Order.Field
public import Mathlib.Topology.Order.T5
import Mathlib.Algebra.Order.Interval.Set.Group
public import Mathlib.Topology.OpenPartialHomeomorph.Defs

/-!
# The additive circle

We define the additive circle `AddCircle p` as the quotient `ğ•œ â§¸ â„¤ âˆ™ p` for some period `p : ğ•œ`.

See also `Circle` and `Real.Angle`.  For the normed group structure on `AddCircle`, see
`AddCircle.NormedAddCommGroup` in a later file.

## Main definitions and results:

* `AddCircle`: the additive circle `ğ•œ â§¸ â„¤ âˆ™ p` for some period `p : ğ•œ`
* `UnitAddCircle`: the special case `â„ â§¸ â„¤`
* `AddCircle.equivAddCircle`: the rescaling equivalence `AddCircle p â‰ƒ+ AddCircle q`
* `AddCircle.equivIco` and `AddCircle.equivIoc`: the natural equivalences
  `AddCircle p â‰ƒ Ico a (a + p)` and `AddCircle p â‰ƒ Ioc a (a + p)`
* `AddCircle.addOrderOf_div_of_gcd_eq_one`: rational points have finite order
* `AddCircle.exists_gcd_eq_one_of_isOfFinAddOrder`: finite-order points are rational
* `AddCircle.homeoIccQuot`: the natural topological equivalence between `AddCircle p` and
  `Icc a (a + p)` with its endpoints identified.
* `AddCircle.liftIco_continuous` and `AddCircle.liftIoc_continuous`: if `f : â„ â†’ B` is continuous,
  and `f a = f (a + p)` for some `a`, then there is a continuous function `AddCircle p â†’ B`
  which agrees with `f` on `Icc a (a + p)`.

## Implementation notes:

Although the most important case is `ğ•œ = â„` we wish to support other types of scalars, such as
the rational circle `AddCircle (1 : â„š)`, and so we set things up more generally.

## TODO

* Link with periodicity
* Lie group structure
* Exponential equivalence to `Circle`

-/

@[expose] public section


noncomputable section

open AddCommGroup Set Function AddSubgroup TopologicalSpace

open Topology

variable {ğ•œ B : Type*}

section Continuity

variable [AddCommGroup ğ•œ] [LinearOrder ğ•œ] [IsOrderedAddMonoid ğ•œ] [Archimedean ğ•œ]
  [TopologicalSpace ğ•œ] [OrderTopology ğ•œ]
  {p : ğ•œ} (hp : 0 < p) (a x : ğ•œ)

/-- `toIcoDiv` is eventually constant on the right at every point. -/
theorem eventuallyEq_toIcoDiv_nhdsGE : toIcoDiv hp a =á¶ [ğ“[â‰¥] x] fun _ â†¦ toIcoDiv hp a x := by
  simp only [Filter.EventuallyEq, toIcoDiv_eq_iff, sub_mem_Ico_iff_left]
  apply Ico_mem_nhdsGE_of_mem
  rw [â† sub_mem_Ico_iff_left, â† toIcoDiv_eq_iff]

/-- `toIcoDiv` is continuous on the right at every point.

In fact, a stronger statement is true:
it's eventually constant on the right, see `eventuallyEq_toIcoDiv_nhdsGE`. -/
theorem continuousWithinAt_toIcoDiv_Ici : ContinuousWithinAt (toIcoDiv hp a) (Ici x) x :=
  Filter.tendsto_pure.mpr (eventuallyEq_toIcoDiv_nhdsGE hp a x) |>.mono_right <| pure_le_nhds _

/-- `toIocDiv` is eventually constant on the left at every point. -/
theorem eventuallyEq_toIocDiv_nhdsLE : toIocDiv hp a =á¶ [ğ“[â‰¤] x] fun _ â†¦ toIocDiv hp a x := by
  simp only [Filter.EventuallyEq, toIocDiv_eq_iff, sub_mem_Ioc_iff_left]
  apply Ioc_mem_nhdsLE_of_mem
  rw [â† sub_mem_Ioc_iff_left, â† toIocDiv_eq_iff]

/-- `toIocDiv` is continuous on the left at every point.

In fact, a stronger statement is true:
it's eventually constant on the left, see `eventuallyEq_toIocDiv_nhdsLE`. -/
theorem continuousWithinAt_toIocDiv_Iic : ContinuousWithinAt (toIocDiv hp a) (Iic x) x :=
  Filter.tendsto_pure.mpr (eventuallyEq_toIocDiv_nhdsLE hp a x) |>.mono_right <| pure_le_nhds _

/-- `toIcoMod` is continuous on the right at every point. -/
theorem continuousWithinAt_toIcoMod_Ici : ContinuousWithinAt (toIcoMod hp a) (Ici x) x :=
  continuousWithinAt_id.sub <|
    (continuousWithinAt_toIcoDiv_Ici hp a x).smul continuousWithinAt_const

@[deprecated (since := "2026-01-04")]
alias continuous_right_toIcoMod := continuousWithinAt_toIcoMod_Ici

/-- `toIocMod` is continuous on the right at every point. -/
theorem continuousWithinAt_toIocMod_Iic : ContinuousWithinAt (toIocMod hp a) (Iic x) x :=
  continuousWithinAt_id.sub <|
    (continuousWithinAt_toIocDiv_Iic hp a x).smul continuousWithinAt_const

@[deprecated (since := "2026-01-04")]
alias continuous_left_toIocMod := continuousWithinAt_toIocMod_Iic

/-- At every point `x`, for all `y < x` sufficiently close to `x`,
we have `toIcoDiv hp a y = toIocDiv hp a x`.

Note that we use different functions on the LHS and on the RHS.
-/
theorem eventuallyEq_toIcoDiv_nhdsLT : toIcoDiv hp a =á¶ [ğ“[<] x] fun _ â†¦ toIocDiv hp a x := by
  simp only [Filter.EventuallyEq, toIcoDiv_eq_iff, sub_mem_Ico_iff_left]
  apply Ico_mem_nhdsLT_of_mem
  rw [â† sub_mem_Ioc_iff_left, â† toIocDiv_eq_iff]

/-- At every point `x`, for all `y > x` sufficiently close to `x`,
we have `toIocDiv hp a y = toIcoDiv hp a x`.

Note that we use different functions on the LHS and on the RHS.
-/
theorem eventuallyEq_toIocDiv_nhdsGT : toIocDiv hp a =á¶ [ğ“[>] x] fun _ â†¦ toIcoDiv hp a x := by
  simp only [Filter.EventuallyEq, toIocDiv_eq_iff, sub_mem_Ioc_iff_left]
  apply Ioc_mem_nhdsGT_of_mem
  rw [â† sub_mem_Ico_iff_left, â† toIcoDiv_eq_iff]

variable {x}

/-- If `x` is not congruent to `a` modulo `p`, then `toIcoDiv` is locally constant near `x`. -/
theorem eventuallyEq_toIcoDiv_nhds (hx : Â¬x â‰¡ a [PMOD p]) :
    toIcoDiv hp a =á¶ [ğ“ x] fun _ â†¦ toIcoDiv hp a x := by
  rw [â† nhdsLT_sup_nhdsGE, Filter.EventuallyEq, Filter.eventually_sup]
  refine âŸ¨?_, eventuallyEq_toIcoDiv_nhdsGE hp a xâŸ©
  convert (eventuallyEq_toIcoDiv_nhdsLT hp a x).eventually using 3
  rwa [â† not_modEq_iff_toIcoDiv_eq_toIocDiv, AddCommGroup.modEq_comm]

/-- If `x` is not congruent to `a` modulo `p`, then `toIcoDiv` is continuous at `x`.

In fact, it is locally near `x`, see `eventuallyEq_toIcoDiv_nhds`. -/
theorem continuousAt_toIcoDiv (hx : Â¬x â‰¡ a [PMOD p]) :
    ContinuousAt (toIcoDiv hp a) x :=
  tendsto_nhds_of_eventually_eq <| eventuallyEq_toIcoDiv_nhds hp a hx

/-- `toIcoDiv` is continuous on the set of points that are not congruent to `a` modulo `p`. -/
theorem continuousOn_toIcoDiv : ContinuousOn (toIcoDiv hp a) {x | Â¬x â‰¡ a [PMOD p]} := fun _x hx â†¦
  (continuousAt_toIcoDiv hp a hx).continuousWithinAt

/-- If `x` is not congruent to `a` modulo `p`, then `toIocDiv` is locally constant near `x`. -/
theorem eventuallyEq_toIocDiv_nhds (hx : Â¬x â‰¡ a [PMOD p]) :
    toIocDiv hp a =á¶ [ğ“ x] fun _ â†¦ toIocDiv hp a x := by
  rw [â† nhdsLE_sup_nhdsGT, Filter.EventuallyEq, Filter.eventually_sup]
  refine âŸ¨eventuallyEq_toIocDiv_nhdsLE hp a x, ?_âŸ©
  convert (eventuallyEq_toIocDiv_nhdsGT hp a x).eventually using 3
  rwa [eq_comm, â† not_modEq_iff_toIcoDiv_eq_toIocDiv, AddCommGroup.modEq_comm]

/-- If `x` is not congruent to `a` modulo `p`, then `toIocDiv` is continuous at `x`.

In fact, it is locally near `x`, see `eventuallyEq_toIocDiv_nhds`. -/
theorem continuousAt_toIocDiv (hx : Â¬x â‰¡ a [PMOD p]) :
    ContinuousAt (toIocDiv hp a) x :=
  tendsto_nhds_of_eventually_eq <| eventuallyEq_toIocDiv_nhds hp a hx

/-- `toIocDiv` is continuous on the set of points
that aren't congruent to the endpoint modulo the period. -/
theorem continuousOn_toIocDiv :
    ContinuousOn (toIocDiv hp a) {x | Â¬x â‰¡ a [PMOD p]} := fun _x hx â†¦
  (continuousAt_toIocDiv hp a hx).continuousWithinAt

theorem toIcoMod_eventuallyEq_toIocMod (hx : Â¬x â‰¡ a [PMOD p]) :
    toIcoMod hp a =á¶ [ğ“ x] toIocMod hp a := by
  refine IsOpen.mem_nhds ?_ ?_
  Â· rw [Ico_eq_locus_Ioc_eq_iUnion_Ioo]
    exact isOpen_iUnion fun i => isOpen_Ioo
  Â· rwa [mem_setOf_eq, â† not_modEq_iff_toIcoMod_eq_toIocMod hp, AddCommGroup.modEq_comm]

theorem continuousAt_toIcoMod (hx : Â¬x â‰¡ a [PMOD p]) : ContinuousAt (toIcoMod hp a) x :=
  continuousAt_id.sub <| tendsto_nhds_of_eventually_eq <|
    (eventuallyEq_toIcoDiv_nhds hp a hx).fun_comp (Â· â€¢ p)

theorem continuousAt_toIocMod (hx : Â¬x â‰¡ a [PMOD p]) : ContinuousAt (toIocMod hp a) x :=
  continuousAt_id.sub <| tendsto_nhds_of_eventually_eq <|
    (eventuallyEq_toIocDiv_nhds hp a hx).fun_comp (Â· â€¢ p)

end Continuity

/-- The "additive circle": `ğ•œ â§¸ â„¤ âˆ™ p`. See also `Circle` and `Real.Angle`. -/
abbrev AddCircle [AddCommGroup ğ•œ] (p : ğ•œ) :=
  ğ•œ â§¸ zmultiples p

namespace AddCircle

section LinearOrderedAddCommGroup

variable [AddCommGroup ğ•œ] (p : ğ•œ)

theorem coe_nsmul {n : â„•} {x : ğ•œ} : (â†‘(n â€¢ x) : AddCircle p) = n â€¢ (x : AddCircle p) :=
  rfl

theorem coe_zsmul {n : â„¤} {x : ğ•œ} : (â†‘(n â€¢ x) : AddCircle p) = n â€¢ (x : AddCircle p) :=
  rfl

theorem coe_add (x y : ğ•œ) : (â†‘(x + y) : AddCircle p) = (x : AddCircle p) + (y : AddCircle p) :=
  rfl

theorem coe_sub (x y : ğ•œ) : (â†‘(x - y) : AddCircle p) = (x : AddCircle p) - (y : AddCircle p) :=
  rfl

theorem coe_neg {x : ğ•œ} : (â†‘(-x) : AddCircle p) = -(x : AddCircle p) :=
  rfl

@[norm_cast]
theorem coe_zero : â†‘(0 : ğ•œ) = (0 : AddCircle p) :=
  rfl

theorem coe_eq_zero_iff {x : ğ•œ} : (x : AddCircle p) = 0 â†” âˆƒ n : â„¤, n â€¢ p = x := by
  simp [AddSubgroup.mem_zmultiples_iff]

theorem coe_period : (p : AddCircle p) = 0 :=
  (QuotientAddGroup.eq_zero_iff p).2 <| mem_zmultiples p

theorem coe_add_period (x : ğ•œ) : ((x + p : ğ•œ) : AddCircle p) = x := by
  rw [coe_add, â† eq_sub_iff_add_eq', sub_self, coe_period]

@[continuity, nolint unusedArguments]
protected theorem continuous_mk' [TopologicalSpace ğ•œ] :
    Continuous (QuotientAddGroup.mk' (zmultiples p) : ğ•œ â†’ AddCircle p) :=
  continuous_coinduced_rng

section Torsion

-- TODO: move this (and the definition `AddCircle`) to GroupTheory.QuotientGroup.Basic
open QuotientAddGroup Cardinal in
theorem card_torsion_le_of_isSMulRegular (n : â„•) (h0 : n â‰  0) (hn : IsSMulRegular ğ•œ n) :
    {x : AddCircle p | n â€¢ x = 0}.encard â‰¤ n := by
  have (x : {x : AddCircle p | n â€¢ x = 0}) : âˆƒ (k : Fin n) (y : ğ•œ), y = x.1 âˆ§ n â€¢ y = k.1 â€¢ p := by
    obtain âŸ¨x, hxâŸ© := x
    obtain âŸ¨y, rflâŸ© := mk_surjective x
    rw [Set.mem_setOf, â† mk_nsmul, eq_zero_iff] at hx
    have âŸ¨m', hmâŸ© := hx
    have : NeZero n := âŸ¨h0âŸ©
    rw [â† (Int.divModEquiv n).symm_apply_apply m', Int.divModEquiv_symm_apply] at hm
    set m := m'.divModEquiv n
    use m.2, y - m.1 â€¢ p
    simp_rw [mk_sub, mk_zsmul, sub_eq_self, coe_period, smul_zero]
    rw [smul_sub, sub_eq_iff_eq_add, â† hm, add_comm]
    simp [add_smul, mul_comm, mul_smul]
  choose f hf using this
  refine (ENat.card_le_card_of_injective (f := f) fun x x' eq â†¦ Subtype.ext ?_).trans (by simp)
  have âŸ¨y, hyx, hyâŸ© := hf x
  have âŸ¨y', hyx', hy'âŸ© := hf x'
  rw [eq, â† hy', hn.eq_iff] at hy
  rw [â† hyx, hy, hyx']

theorem finite_torsion_of_isSMulRegular (n : â„•) (hn : IsSMulRegular ğ•œ n) :
    {x : AddCircle p | n â€¢ x = 0}.Finite := by
  nontriviality ğ•œ
  obtain rfl | h0 := eq_or_ne n 0
  exacts [hn.not_zero.elim, ENat.card_lt_top.mp <|
    (card_torsion_le_of_isSMulRegular p n h0 hn).trans_lt <| ENat.coe_lt_top n]

theorem card_torsion_le_of_isSMulRegular_int (n : â„¤) (h0 : n â‰  0) (hn : IsSMulRegular ğ•œ n) :
    {x : AddCircle p | n â€¢ x = 0}.encard â‰¤ n.natAbs := by
  convert card_torsion_le_of_isSMulRegular p _
    (Int.natAbs_ne_zero.mpr h0) (IsSMulRegular.natAbs_iff.mpr hn) using 1
  conv_lhs => rw [â† n.sign_mul_natAbs]
  obtain h | h | h := n.sign_trichotomy
  Â· simp [h]
  Â· exact (h0 <| by simpa using h).elim
  Â· simp [h]

theorem finite_torsion_of_isSMulRegular_int (n : â„¤) (hn : IsSMulRegular ğ•œ n) :
    {x : AddCircle p | n â€¢ x = 0}.Finite := by
  nontriviality ğ•œ
  obtain rfl | h0 := eq_or_ne n 0
  exacts [hn.not_zero.elim, ENat.card_lt_top.mp <|
    (card_torsion_le_of_isSMulRegular_int p n h0 hn).trans_lt <| ENat.coe_lt_top _]

end Torsion

variable [LinearOrder ğ•œ] [IsOrderedAddMonoid ğ•œ]

theorem finite_torsion {n : â„•} (hn : 0 < n) : { u : AddCircle p | n â€¢ u = 0 }.Finite :=
  finite_torsion_of_isSMulRegular _ _ <| .of_right_eq_zero_of_smul fun _ â†¦ by simp [hn.ne']

theorem finite_setOf_addOrderOf_eq {n : â„•} (hn : 0 < n) :
    {u : AddCircle p | addOrderOf u = n}.Finite :=
  (finite_torsion p hn).subset fun _ h â†¦ ((addOrderOf_eq_iff hn).mp h).1

theorem coe_eq_zero_of_pos_iff (hp : 0 < p) {x : ğ•œ} (hx : 0 < x) :
    (x : AddCircle p) = 0 â†” âˆƒ n : â„•, n â€¢ p = x := by
  rw [coe_eq_zero_iff]
  constructor <;> rintro âŸ¨n, rflâŸ©
  Â· replace hx : 0 < n := by
      contrapose! hx
      simpa only [â† neg_nonneg, â† zsmul_neg, zsmul_neg'] using zsmul_nonneg hp.le (neg_nonneg.2 hx)
    exact âŸ¨n.toNat, by rw [â† natCast_zsmul, Int.toNat_of_nonneg hx.le]âŸ©
  Â· exact âŸ¨(n : â„¤), by simpâŸ©

variable [hp : Fact (0 < p)] (a : ğ•œ) [Archimedean ğ•œ]

/-- The equivalence between `AddCircle p` and the half-open interval `[a, a + p)`, whose inverse
is the natural quotient map. -/
def equivIco : AddCircle p â‰ƒ Ico a (a + p) :=
  QuotientAddGroup.equivIcoMod hp.out a

/-- The equivalence between `AddCircle p` and the half-open interval `(a, a + p]`, whose inverse
is the natural quotient map. -/
def equivIoc : AddCircle p â‰ƒ Ioc a (a + p) :=
  QuotientAddGroup.equivIocMod hp.out a

/-- Given a function on `ğ•œ`, return the unique function on `AddCircle p` agreeing with `f` on
`[a, a + p)`. -/
def liftIco (f : ğ•œ â†’ B) : AddCircle p â†’ B :=
  restrict _ f âˆ˜ AddCircle.equivIco p a

/-- Given a function on `ğ•œ`, return the unique function on `AddCircle p` agreeing with `f` on
`(a, a + p]`. -/
def liftIoc (f : ğ•œ â†’ B) : AddCircle p â†’ B :=
  restrict _ f âˆ˜ AddCircle.equivIoc p a

variable {p a}

theorem equivIco_coe_eq {x : ğ•œ} (hx : x âˆˆ Ico a (a + p)) : (equivIco p a) x = âŸ¨x, hxâŸ© := by
  rw [Equiv.apply_eq_iff_eq_symm_apply, equivIco, QuotientAddGroup.equivIcoMod_symm_apply]

theorem equivIoc_coe_eq {x : ğ•œ} (hx : x âˆˆ Ioc a (a + p)) : (equivIoc p a) x = âŸ¨x, hxâŸ© := by
  rw [Equiv.apply_eq_iff_eq_symm_apply, equivIoc, QuotientAddGroup.equivIocMod_symm_apply]

theorem coe_eq_coe_iff_of_mem_Ico {x y : ğ•œ} (hx : x âˆˆ Ico a (a + p)) (hy : y âˆˆ Ico a (a + p)) :
    (x : AddCircle p) = y â†” x = y := by
  refine âŸ¨fun h => ?_, by tautoâŸ©
  suffices (âŸ¨x, hxâŸ© : Ico a (a + p)) = âŸ¨y, hyâŸ© by exact Subtype.mk.inj this
  apply_fun equivIco p a at h
  rw [â† (equivIco p a).right_inv âŸ¨x, hxâŸ©, â† (equivIco p a).right_inv âŸ¨y, hyâŸ©]
  exact h

theorem liftIco_coe_apply {f : ğ•œ â†’ B} {x : ğ•œ} (hx : x âˆˆ Ico a (a + p)) :
    liftIco p a f â†‘x = f x := by
  simp [liftIco, equivIco_coe_eq hx]

theorem liftIoc_coe_apply {f : ğ•œ â†’ B} {x : ğ•œ} (hx : x âˆˆ Ioc a (a + p)) :
    liftIoc p a f â†‘x = f x := by
  simp [liftIoc, equivIoc_coe_eq hx]

lemma liftIco_comp_apply {Î± Î² : Type*} {f : ğ•œ â†’ Î±} {g : Î± â†’ Î²} {a : ğ•œ} {x : AddCircle p} :
    liftIco p a (g âˆ˜ f) x = g (liftIco p a f x) := rfl

lemma liftIoc_comp_apply {Î± Î² : Type*} {f : ğ•œ â†’ Î±} {g : Î± â†’ Î²} {a : ğ•œ} {x : AddCircle p} :
    liftIoc p a (g âˆ˜ f) x = g (liftIoc p a f x) := rfl

lemma eq_coe_Ico (a : AddCircle p) : âˆƒ b, b âˆˆ Ico 0 p âˆ§ â†‘b = a := by
  let b := QuotientAddGroup.equivIcoMod hp.out 0 a
  exact âŸ¨b.1, by simpa only [zero_add] using b.2,
    (QuotientAddGroup.equivIcoMod hp.out 0).symm_apply_apply aâŸ©

lemma coe_eq_zero_iff_of_mem_Ico (ha : a âˆˆ Ico 0 p) :
    (a : AddCircle p) = 0 â†” a = 0 := by
  have h0 : 0 âˆˆ Ico 0 (0 + p) := by simpa [zero_add, left_mem_Ico] using hp.out
  have ha' : a âˆˆ Ico 0 (0 + p) := by rwa [zero_add]
  rw [â† AddCircle.coe_eq_coe_iff_of_mem_Ico ha' h0, QuotientAddGroup.mk_zero]

variable (p a)

section Continuity

variable [TopologicalSpace ğ•œ]

@[continuity]
theorem continuous_equivIco_symm : Continuous (equivIco p a).symm :=
  continuous_quotient_mk'.comp continuous_subtype_val

@[continuity]
theorem continuous_equivIoc_symm : Continuous (equivIoc p a).symm :=
  continuous_quotient_mk'.comp continuous_subtype_val

variable [OrderTopology ğ•œ] {x : AddCircle p}

theorem continuousAt_equivIco (hx : x â‰  a) : ContinuousAt (equivIco p a) x := by
  induction x using QuotientAddGroup.induction_on
  rw [ContinuousAt, Filter.Tendsto, QuotientAddGroup.nhds_eq, Filter.map_map]
  exact (continuousAt_toIcoMod hp.out a <| not_modEq_iff_ne_mod_zmultiples.mpr hx).codRestrict _

theorem continuousAt_equivIoc (hx : x â‰  a) : ContinuousAt (equivIoc p a) x := by
  induction x using QuotientAddGroup.induction_on
  rw [ContinuousAt, Filter.Tendsto, QuotientAddGroup.nhds_eq, Filter.map_map]
  exact (continuousAt_toIocMod hp.out a <| not_modEq_iff_ne_mod_zmultiples.mpr hx).codRestrict _

/-- The quotient map `ğ•œ â†’ AddCircle p` as an open partial homeomorphism. -/
@[simps] def openPartialHomeomorphCoe [DiscreteTopology (zmultiples p)] :
    OpenPartialHomeomorph ğ•œ (AddCircle p) where
  toFun := (â†‘)
  invFun := fun x â†¦ equivIco p a x
  source := Ioo a (a + p)
  target := {â†‘a}á¶œ
  map_source' := by
    intro x hx hx'
    exact hx.1.ne' ((coe_eq_coe_iff_of_mem_Ico (Ioo_subset_Ico_self hx)
      (left_mem_Ico.mpr (lt_add_of_pos_right a hp.out))).mp hx')
  map_target' := by
    intro x hx
    exact (eq_left_or_mem_Ioo_of_mem_Ico (equivIco p a x).2).resolve_left
      (hx âˆ˜ ((equivIco p a).symm_apply_apply x).symm.trans âˆ˜ congrArg _)
  left_inv' :=
    fun x hx â†¦ congrArg _ ((equivIco p a).apply_symm_apply âŸ¨x, Ioo_subset_Ico_self hxâŸ©)
  right_inv' := fun x _ â†¦ (equivIco p a).symm_apply_apply x
  open_source := isOpen_Ioo
  open_target := isOpen_compl_singleton
  continuousOn_toFun := (AddCircle.continuous_mk' p).continuousOn
  continuousOn_invFun := by
    exact continuousOn_of_forall_continuousAt
      (fun _ â†¦ continuousAt_subtype_val.comp âˆ˜ continuousAt_equivIco p a)

@[deprecated (since := "2025-08-29")] noncomputable alias
  partialHomeomorphCoe := openPartialHomeomorphCoe

end Continuity

/-- The image of the closed-open interval `[a, a + p)` under the quotient map `ğ•œ â†’ AddCircle p` is
the entire space. -/
@[simp]
theorem coe_image_Ico_eq : ((â†‘) : ğ•œ â†’ AddCircle p) '' Ico a (a + p) = univ := by
  rw [image_eq_range]
  exact (equivIco p a).symm.range_eq_univ

/-- The image of the closed-open interval `[a, a + p)` under the quotient map `ğ•œ â†’ AddCircle p` is
the entire space. -/
@[simp]
theorem coe_image_Ioc_eq : ((â†‘) : ğ•œ â†’ AddCircle p) '' Ioc a (a + p) = univ := by
  rw [image_eq_range]
  exact (equivIoc p a).symm.range_eq_univ

/-- The image of the closed interval `[0, p]` under the quotient map `ğ•œ â†’ AddCircle p` is the
entire space. -/
@[simp]
theorem coe_image_Icc_eq : ((â†‘) : ğ•œ â†’ AddCircle p) '' Icc a (a + p) = univ :=
  eq_top_mono (image_mono Ico_subset_Icc_self) <| coe_image_Ico_eq _ _

/-- If functions on AddCircle agree on the image of the interval `[a, a + p)` then they are equal -/
lemma Ico_ext {Î± : Type*} {f g : AddCircle p â†’ Î±} (a : ğ•œ)
    (h : âˆ€ x âˆˆ Ico a (a + p), f x = g x) : f = g := by
  rw [â† Set.eqOn_univ, â† coe_image_Ico_eq p a]
  rintro - âŸ¨x, hx, rflâŸ©
  exact h x hx

/-- If functions on AddCircle agree on the image of the interval `(a, a + p]` then they are equal -/
lemma Ioc_ext {Î± : Type*} {f g : AddCircle p â†’ Î±} (a : ğ•œ)
    (h : âˆ€ x âˆˆ Ioc a (a + p), f x = g x) : f = g := by
  rw [â† Set.eqOn_univ, â† coe_image_Ioc_eq p a]
  rintro - âŸ¨x, hx, rflâŸ©
  exact h x hx

end LinearOrderedAddCommGroup

section LinearOrderedField

variable [Field ğ•œ] (p q : ğ•œ)

/-- The rescaling equivalence between additive circles with different periods. -/
def equivAddCircle (hp : p â‰  0) (hq : q â‰  0) : AddCircle p â‰ƒ+ AddCircle q :=
  QuotientAddGroup.congr _ _ (AddAut.mulRight <| (Units.mk0 p hp)â»Â¹ * Units.mk0 q hq) <| by
    rw [AddMonoidHom.map_zmultiples, AddMonoidHom.coe_coe, AddAut.mulRight_apply, Units.val_mul,
      Units.val_mk0, Units.val_inv_eq_inv_val, Units.val_mk0, mul_inv_cancel_leftâ‚€ hp]

@[simp]
theorem equivAddCircle_apply_mk (hp : p â‰  0) (hq : q â‰  0) (x : ğ•œ) :
    equivAddCircle p q hp hq (x : ğ•œ) = (x * (pâ»Â¹ * q) : ğ•œ) :=
  rfl

@[simp]
theorem equivAddCircle_symm_apply_mk (hp : p â‰  0) (hq : q â‰  0) (x : ğ•œ) :
    (equivAddCircle p q hp hq).symm (x : ğ•œ) = (x * (qâ»Â¹ * p) : ğ•œ) :=
  rfl

section
variable [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [TopologicalSpace ğ•œ] [OrderTopology ğ•œ]

/-- The rescaling homeomorphism between additive circles with different periods. -/
def homeomorphAddCircle (hp : p â‰  0) (hq : q â‰  0) : AddCircle p â‰ƒâ‚œ AddCircle q :=
  âŸ¨equivAddCircle p q hp hq,
    (continuous_quotient_mk'.comp (continuous_mul_right (pâ»Â¹ * q))).quotient_lift _,
    (continuous_quotient_mk'.comp (continuous_mul_right (qâ»Â¹ * p))).quotient_lift _âŸ©

@[simp]
theorem homeomorphAddCircle_apply_mk (hp : p â‰  0) (hq : q â‰  0) (x : ğ•œ) :
    homeomorphAddCircle p q hp hq (x : ğ•œ) = (x * (pâ»Â¹ * q) : ğ•œ) :=
  rfl

@[simp]
theorem homeomorphAddCircle_symm_apply_mk (hp : p â‰  0) (hq : q â‰  0) (x : ğ•œ) :
    (homeomorphAddCircle p q hp hq).symm (x : ğ•œ) = (x * (qâ»Â¹ * p) : ğ•œ) :=
  rfl
end

lemma natCast_div_mul_eq_nsmul (r : ğ•œ) (m : â„•) :
    (â†‘(â†‘m / q * r) : AddCircle p) = m â€¢ (r / q : AddCircle p) := by
  rw [mul_comm_div, â† nsmul_eq_mul, coe_nsmul]

lemma intCast_div_mul_eq_zsmul (r : ğ•œ) (m : â„¤) :
    (â†‘(â†‘m / q * r) : AddCircle p) = m â€¢ (r / q : AddCircle p) := by
  rw [mul_comm_div, â† zsmul_eq_mul, coe_zsmul]

variable [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [hp : Fact (0 < p)]

section FloorRing

variable [FloorRing ğ•œ]

@[simp]
theorem coe_equivIco_mk_apply (x : ğ•œ) :
    (equivIco p 0 <| QuotientAddGroup.mk x : ğ•œ) = Int.fract (x / p) * p :=
  toIcoMod_eq_fract_mul _ x

instance : DivisibleBy (AddCircle p) â„¤ where
  div x n := (â†‘((n : ğ•œ)â»Â¹ * (equivIco p 0 x : ğ•œ)) : AddCircle p)
  div_zero x := by simp
  div_cancel {n} x hn := by
    replace hn : (n : ğ•œ) â‰  0 := by norm_cast
    change n â€¢ QuotientAddGroup.mk' _ ((n : ğ•œ)â»Â¹ * â†‘(equivIco p 0 x)) = x
    rw [â† map_zsmul, â† smul_mul_assoc, zsmul_eq_mul, mul_inv_cancelâ‚€ hn, one_mul]
    exact (equivIco p 0).symm_apply_apply x

omit [IsStrictOrderedRing ğ•œ] in
@[simp] lemma coe_fract (x : ğ•œ) : (â†‘(Int.fract x) : AddCircle (1 : ğ•œ)) = x := by
  simp [â† Int.self_sub_floor, mem_zmultiples_iff]

end FloorRing

section FiniteOrderPoints

variable {p}

theorem addOrderOf_period_div {n : â„•} (h : 0 < n) : addOrderOf ((p / n : ğ•œ) : AddCircle p) = n := by
  rw [addOrderOf_eq_iff h]
  replace h : 0 < (n : ğ•œ) := Nat.cast_pos.2 h
  refine âŸ¨?_, fun m hn h0 => ?_âŸ© <;> simp only [Ne, â† coe_nsmul, nsmul_eq_mul]
  Â· rw [mul_div_cancelâ‚€ _ h.ne', coe_period]
  rw [coe_eq_zero_of_pos_iff p hp.out (mul_pos (Nat.cast_pos.2 h0) <| div_pos hp.out h)]
  rintro âŸ¨k, hkâŸ©
  rw [mul_div, eq_div_iff h.ne', nsmul_eq_mul, mul_right_comm, â† Nat.cast_mul,
    (mul_left_injectiveâ‚€ hp.out.ne').eq_iff, Nat.cast_inj, mul_comm] at hk
  exact (Nat.le_of_dvd h0 âŸ¨_, hk.symmâŸ©).not_gt hn

variable (p) in
theorem gcd_mul_addOrderOf_div_eq {n : â„•} (m : â„•) (hn : 0 < n) :
    m.gcd n * addOrderOf (â†‘(â†‘m / â†‘n * p) : AddCircle p) = n := by
  rw [natCast_div_mul_eq_nsmul, IsOfFinAddOrder.addOrderOf_nsmul]
  Â· rw [addOrderOf_period_div hn, Nat.gcd_comm, Nat.mul_div_cancel']
    exact n.gcd_dvd_left m
  Â· rwa [â† addOrderOf_pos_iff, addOrderOf_period_div hn]

theorem addOrderOf_div_of_gcd_eq_one {m n : â„•} (hn : 0 < n) (h : m.gcd n = 1) :
    addOrderOf (â†‘(â†‘m / â†‘n * p) : AddCircle p) = n := by
  convert gcd_mul_addOrderOf_div_eq p m hn
  rw [h, one_mul]

theorem addOrderOf_div_of_gcd_eq_one' {m : â„¤} {n : â„•} (hn : 0 < n) (h : m.natAbs.gcd n = 1) :
    addOrderOf (â†‘(â†‘m / â†‘n * p) : AddCircle p) = n := by
  cases m
  Â· simp only [Int.ofNat_eq_natCast, Int.cast_natCast, Int.natAbs_natCast] at h âŠ¢
    exact addOrderOf_div_of_gcd_eq_one hn h
  Â· simp only [Int.cast_negSucc, neg_div, neg_mul, coe_neg, addOrderOf_neg]
    exact addOrderOf_div_of_gcd_eq_one hn h

theorem addOrderOf_coe_rat {q : â„š} : addOrderOf (â†‘(â†‘q * p) : AddCircle p) = q.den := by
  have : (â†‘(q.den : â„¤) : ğ•œ) â‰  0 := by
    norm_cast
    exact q.pos.ne.symm
  rw [â† q.num_divInt_den, Rat.cast_divInt_of_ne_zero _ this, Int.cast_natCast, Rat.num_divInt_den,
    addOrderOf_div_of_gcd_eq_one' q.pos q.reduced]

protected theorem nsmul_eq_zero_iff {u : AddCircle p} {n : â„•} (h : 0 < n) :
    n â€¢ u = 0 â†” âˆƒ m < n, â†‘(â†‘m / â†‘n * p) = u := by
  refine âŸ¨QuotientAddGroup.induction_on u fun k hk â†¦ ?_, ?_âŸ©
  Â· rw [â† addOrderOf_dvd_iff_nsmul_eq_zero]
    rintro âŸ¨m, -, rflâŸ©
    constructor; rw [mul_comm, eq_comm]
    exact gcd_mul_addOrderOf_div_eq p m h
  rw [â† coe_nsmul, coe_eq_zero_iff] at hk
  obtain âŸ¨a, haâŸ© := hk
  refine âŸ¨a.natMod n, Int.natMod_lt h.ne', ?_âŸ©
  have h0 : (n : ğ•œ) â‰  0 := Nat.cast_ne_zero.2 h.ne'
  rw [nsmul_eq_mul, mul_comm, â† div_eq_iff h0, â† a.ediv_mul_add_emod n, add_smul, add_div,
    zsmul_eq_mul, Int.cast_mul, Int.cast_natCast, mul_assoc, â† mul_div, mul_comm _ p,
    mul_div_cancel_rightâ‚€ p h0] at ha
  rw [â† ha, coe_add, â† Int.cast_natCast, Int.natMod, Int.toNat_of_nonneg, zsmul_eq_mul,
    mul_div_right_comm, eq_comm, add_eq_right, â† zsmul_eq_mul, coe_zsmul, coe_period, smul_zero]
  exact Int.emod_nonneg _ (by exact_mod_cast h.ne')

theorem addOrderOf_eq_pos_iff {u : AddCircle p} {n : â„•} (h : 0 < n) :
    addOrderOf u = n â†” âˆƒ m < n, m.gcd n = 1 âˆ§ â†‘(â†‘m / â†‘n * p) = u := by
  refine âŸ¨QuotientAddGroup.induction_on u ?_, ?_âŸ©
  Â· rintro âŸ¨m, -, hâ‚, rflâŸ©
    exact addOrderOf_div_of_gcd_eq_one h hâ‚
  rintro k rfl
  obtain âŸ¨m, hm, hkâŸ© := (AddCircle.nsmul_eq_zero_iff h).mp
    (addOrderOf_nsmul_eq_zero (k : AddCircle p))
  refine âŸ¨m, hm, mul_right_cancelâ‚€ h.ne' ?_, hkâŸ©
  convert gcd_mul_addOrderOf_div_eq p m h using 1
  Â· rw [hk]
  Â· apply one_mul

theorem exists_gcd_eq_one_of_isOfFinAddOrder {u : AddCircle p} (h : IsOfFinAddOrder u) :
    âˆƒ m : â„•, m.gcd (addOrderOf u) = 1 âˆ§ m < addOrderOf u âˆ§ â†‘((m : ğ•œ) / addOrderOf u * p) = u :=
  let âŸ¨m, hl, hg, heâŸ© := (addOrderOf_eq_pos_iff h.addOrderOf_pos).1 rfl
  âŸ¨m, hg, hl, heâŸ©

lemma not_isOfFinAddOrder_iff_forall_rat_ne_div {a : ğ•œ} :
    Â¬ IsOfFinAddOrder (a : AddCircle p) â†” âˆ€ q : â„š, (q : ğ•œ) â‰  a / p := by
  simp +contextual [â† QuotientAddGroup.mk_zsmul, mul_comm (Int.cast _), mem_zmultiples_iff,
    eq_div_iff (Fact.out : 0 < p).ne', isOfFinAddOrder_iff_zsmul_eq_zero, Rat.forall, div_eq_iff,
    div_mul_eq_mul_div]
  grind

lemma isOfFinAddOrder_iff_exists_rat_eq_div {a : ğ•œ} :
    IsOfFinAddOrder (a : AddCircle p) â†” âˆƒ q : â„š, (q : ğ•œ) = a / p := by
  simpa using not_isOfFinAddOrder_iff_forall_rat_ne_div.not_right

@[deprecated not_isOfFinAddOrder_iff_forall_rat_ne_div (since := "2025-08-13")]
theorem addOrderOf_coe_eq_zero_iff_forall_rat_ne_div {a : ğ•œ} :
    addOrderOf (a : AddCircle p) = 0 â†” âˆ€ q : â„š, (q : ğ•œ) â‰  a / p := by
  simp [not_isOfFinAddOrder_iff_forall_rat_ne_div]

variable (p)

/-- The natural bijection between points of order `n` and natural numbers less than and coprime to
`n`. The inverse of the map sends `m â†¦ (m/n * p : AddCircle p)` where `m` is coprime to `n` and
satisfies `0 â‰¤ m < n`. -/
def setAddOrderOfEquiv {n : â„•} (hn : 0 < n) :
    { u : AddCircle p | addOrderOf u = n } â‰ƒ { m | m < n âˆ§ m.gcd n = 1 } :=
  Equiv.symm <|
    Equiv.ofBijective (fun m => âŸ¨â†‘((m : ğ•œ) / n * p), addOrderOf_div_of_gcd_eq_one hn m.prop.2âŸ©)
      (by
        refine âŸ¨fun mâ‚ mâ‚‚ h => Subtype.ext ?_, fun u => ?_âŸ©
        Â· simp_rw [Subtype.mk_eq_mk, natCast_div_mul_eq_nsmul] at h
          refine nsmul_injOn_Iio_addOrderOf ?_ ?_ h <;> rw [addOrderOf_period_div hn]
          exacts [mâ‚.2.1, mâ‚‚.2.1]
        Â· obtain âŸ¨m, hmn, hg, heâŸ© := (addOrderOf_eq_pos_iff hn).mp u.2
          exact âŸ¨âŸ¨m, hmn, hgâŸ©, Subtype.ext heâŸ©)

@[simp]
theorem card_addOrderOf_eq_totient {n : â„•} :
    Nat.card { u : AddCircle p // addOrderOf u = n } = n.totient := by
  rcases n.eq_zero_or_pos with (rfl | hn)
  Â· simp only [Nat.totient_zero, addOrderOf_eq_zero_iff]
    rcases em (âˆƒ u : AddCircle p, Â¬IsOfFinAddOrder u) with (âŸ¨u, huâŸ© | h)
    Â· have : Infinite { u : AddCircle p // Â¬IsOfFinAddOrder u } := by
        rw [â† coe_setOf, infinite_coe_iff]
        exact infinite_not_isOfFinAddOrder hu
      exact Nat.card_eq_zero_of_infinite
    Â· have : IsEmpty { u : AddCircle p // Â¬IsOfFinAddOrder u } := by simpa [isEmpty_subtype] using h
      exact Nat.card_of_isEmpty
  Â· rw [â† coe_setOf, Nat.card_congr (setAddOrderOfEquiv p hn),
      n.totient_eq_card_lt_and_coprime]
    simp only [Nat.gcd_comm]

end FiniteOrderPoints

end LinearOrderedField

end AddCircle

section IdentifyIccEnds

/-! This section proves that for any `a`, the natural map from `[a, a + p] âŠ‚ ğ•œ` to `AddCircle p`
gives an identification of `AddCircle p`, as a topological space, with the quotient of `[a, a + p]`
by the equivalence relation identifying the endpoints. -/

namespace AddCircle

variable [AddCommGroup ğ•œ] [LinearOrder ğ•œ] [IsOrderedAddMonoid ğ•œ] (p a : ğ•œ)
  [hp : Fact (0 < p)]

local notation "ğ•‹" => AddCircle p

/-- The relation identifying the endpoints of `Icc a (a + p)`. -/
inductive EndpointIdent : Icc a (a + p) â†’ Icc a (a + p) â†’ Prop
  | mk :
    EndpointIdent âŸ¨a, left_mem_Icc.mpr <| le_add_of_nonneg_right hp.out.leâŸ©
      âŸ¨a + p, right_mem_Icc.mpr <| le_add_of_nonneg_right hp.out.leâŸ©

variable [Archimedean ğ•œ]

/-- The equivalence between `AddCircle p` and the quotient of `[a, a + p]` by the relation
identifying the endpoints. -/
def equivIccQuot : ğ•‹ â‰ƒ Quot (EndpointIdent p a) where
  toFun x := Quot.mk _ <| inclusion Ico_subset_Icc_self (equivIco _ _ x)
  invFun x :=
    Quot.liftOn x (â†‘) <| by
      rintro _ _ âŸ¨_âŸ©
      exact (coe_add_period p a).symm
  left_inv := (equivIco p a).symm_apply_apply
  right_inv :=
    Quot.ind <| by
      rintro âŸ¨x, hxâŸ©
      rcases ne_or_eq x (a + p) with (h | rfl)
      Â· revert x
        dsimp only
        intro x hx h
        congr
        ext1
        apply congr_arg Subtype.val ((equivIco p a).right_inv âŸ¨x, hx.1, hx.2.lt_of_ne hâŸ©)
      Â· rw [â† Quot.sound EndpointIdent.mk]
        dsimp only
        congr
        ext1
        apply congr_arg Subtype.val
          ((equivIco p a).right_inv âŸ¨a, le_refl a, lt_add_of_pos_right a hp.outâŸ©)

theorem equivIccQuot_comp_mk_eq_toIcoMod :
    equivIccQuot p a âˆ˜ Quotient.mk'' = fun x =>
      Quot.mk _ âŸ¨toIcoMod hp.out a x, Ico_subset_Icc_self <| toIcoMod_mem_Ico _ _ xâŸ© :=
  rfl

theorem equivIccQuot_comp_mk_eq_toIocMod :
    equivIccQuot p a âˆ˜ Quotient.mk'' = fun x =>
      Quot.mk _ âŸ¨toIocMod hp.out a x, Ioc_subset_Icc_self <| toIocMod_mem_Ioc _ _ xâŸ© := by
  rw [equivIccQuot_comp_mk_eq_toIcoMod]
  funext x
  by_cases h : a â‰¡ x [PMOD p]
  Â· simp_rw [(modEq_iff_toIcoMod_eq_left hp.out).1 h, (modEq_iff_toIocMod_eq_right hp.out).1 h]
    exact Quot.sound EndpointIdent.mk
  Â· simp_rw [(not_modEq_iff_toIcoMod_eq_toIocMod hp.out).1 h]

/-- The natural map from `[a, a + p] âŠ‚ ğ•œ` with endpoints identified to `ğ•œ / â„¤ â€¢ p`, as a
homeomorphism of topological spaces. -/
def homeoIccQuot [TopologicalSpace ğ•œ] [OrderTopology ğ•œ] : ğ•‹ â‰ƒâ‚œ Quot (EndpointIdent p a) where
  toEquiv := equivIccQuot p a
  continuous_toFun := by
    simp_rw [isQuotientMap_quotient_mk'.continuous_iff, continuous_iff_continuousAt,
      continuousAt_iff_continuous_left_right]
    intro x; constructor
    on_goal 1 => erw [equivIccQuot_comp_mk_eq_toIocMod]
    on_goal 2 => erw [equivIccQuot_comp_mk_eq_toIcoMod]
    all_goals
      apply continuous_quot_mk.continuousAt.comp_continuousWithinAt
      rw [IsInducing.subtypeVal.continuousWithinAt_iff]
    Â· apply continuousWithinAt_toIocMod_Iic
    Â· apply continuousWithinAt_toIcoMod_Ici
  continuous_invFun :=
    continuous_quot_lift _ ((AddCircle.continuous_mk' p).comp continuous_subtype_val)

/-! We now show that a continuous function on `[a, a + p]` satisfying `f a = f (a + p)` is the
pullback of a continuous function on `AddCircle p`, by first showing that
various lifts are equivalent. -/


variable {p a}

theorem liftIoc_eq_liftIco {f : ğ•œ â†’ B} (hf : f a = f (a + p)) :
    liftIoc p a f = liftIco p a f := by
  ext q
  obtain âŸ¨x, hx, rflâŸ© := by simpa only [mem_image] using coe_image_Ico_eq p a â–¸ mem_univ q
  rw [liftIco_coe_apply hx]
  obtain (âŸ¨rfl, -âŸ© | h) := by rwa [mem_Ico, le_iff_eq_or_lt, or_and_right] at hx
  Â· rw [â† coe_add_period, liftIoc_coe_apply (by simp [hp.out]), hf]
  Â· exact liftIoc_coe_apply âŸ¨h.1, h.2.leâŸ©

theorem liftIco_eq_lift_Icc {f : ğ•œ â†’ B} (h : f a = f (a + p)) :
    liftIco p a f =
      Quot.lift (restrict (Icc a <| a + p) f)
          (by
            rintro _ _ âŸ¨_âŸ©
            exact h) âˆ˜
        equivIccQuot p a :=
  rfl

theorem liftIoc_eq_lift_Icc {f : ğ•œ â†’ B} (h : f a = f (a + p)) :
    liftIoc p a f =
      Quot.lift (restrict (Icc a <| a + p) f)
          (by
            rintro _ _ âŸ¨_âŸ©
            exact h) âˆ˜
        equivIccQuot p a := by
  rw [â† liftIco_eq_lift_Icc h]
  exact liftIoc_eq_liftIco h

theorem liftIco_zero_coe_apply {f : ğ•œ â†’ B} {x : ğ•œ} (hx : x âˆˆ Ico 0 p) : liftIco p 0 f â†‘x = f x :=
  liftIco_coe_apply (by rwa [zero_add])

theorem liftIoc_zero_coe_apply {f : ğ•œ â†’ B} {x : ğ•œ} (hx : x âˆˆ Ioc 0 p) : liftIoc p 0 f â†‘x = f x :=
  liftIoc_coe_apply (by rwa [zero_add])

variable [TopologicalSpace ğ•œ] [OrderTopology ğ•œ]

theorem liftIco_continuous [TopologicalSpace B] {f : ğ•œ â†’ B} (hf : f a = f (a + p))
    (hc : ContinuousOn f <| Icc a (a + p)) : Continuous (liftIco p a f) := by
  rw [liftIco_eq_lift_Icc hf]
  refine Continuous.comp ?_ (homeoIccQuot p a).continuous_toFun
  exact continuous_coinduced_dom.mpr (continuousOn_iff_continuous_restrict.mp hc)

theorem liftIco_zero_continuous [TopologicalSpace B] {f : ğ•œ â†’ B} (hf : f 0 = f p)
    (hc : ContinuousOn f <| Icc 0 p) : Continuous (liftIco p 0 f) :=
  liftIco_continuous (by rwa [zero_add] : f 0 = f (0 + p)) (by rwa [zero_add])

theorem liftIoc_continuous [TopologicalSpace B] {f : ğ•œ â†’ B} (hf : f a = f (a + p))
    (hc : ContinuousOn f <| Icc a (a + p)) : Continuous (liftIoc p a f) := by
  rw [liftIoc_eq_lift_Icc hf]
  refine Continuous.comp ?_ (homeoIccQuot p a).continuous_toFun
  exact continuous_coinduced_dom.mpr (continuousOn_iff_continuous_restrict.mp hc)

theorem liftIoc_zero_continuous [TopologicalSpace B] {f : ğ•œ â†’ B} (hf : f 0 = f p)
    (hc : ContinuousOn f <| Icc 0 p) : Continuous (liftIoc p 0 f) :=
  liftIoc_continuous (by rwa [zero_add] : f 0 = f (0 + p)) (by rwa [zero_add])

end AddCircle

end IdentifyIccEnds
