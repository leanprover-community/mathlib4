/-
Copyright (c) 2018 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin
-/
import Mathlib.Data.NNReal.Basic
import Mathlib.Topology.Algebra.InfiniteSum.Order
import Mathlib.Topology.Algebra.InfiniteSum.Ring
import Mathlib.Topology.Algebra.Ring.Real
import Mathlib.Topology.ContinuousMap.Basic

/-!
# Topology on `‚Ñù‚â•0`

The basic lemmas for the natural topology on `‚Ñù‚â•0` .

## Main statements

Various mathematically trivial lemmas are proved about the compatibility
of limits and sums in `‚Ñù‚â•0` and `‚Ñù`. For example

* `tendsto_coe {f : Filter Œ±} {m : Œ± ‚Üí ‚Ñù‚â•0} {x : ‚Ñù‚â•0} :
  Filter.Tendsto (fun a, (m a : ‚Ñù)) f (ùìù (x : ‚Ñù)) ‚Üî Filter.Tendsto m f (ùìù x)`

says that the limit of a filter along a map to `‚Ñù‚â•0` is the same in `‚Ñù` and `‚Ñù‚â•0`, and

* `coe_tsum {f : Œ± ‚Üí ‚Ñù‚â•0} : ((‚àë'a, f a) : ‚Ñù) = (‚àë'a, (f a : ‚Ñù))`

says that says that a sum of elements in `‚Ñù‚â•0` is the same in `‚Ñù` and `‚Ñù‚â•0`.

Similarly, some mathematically trivial lemmas about infinite sums are proved,
a few of which rely on the fact that subtraction is continuous.

-/

noncomputable section

open Filter Metric Set TopologicalSpace Topology

variable {Œπ : Sort*} {n : ‚Ñï}

namespace NNReal

variable {Œ± : Type*} {L : SummationFilter Œ±}

section coe

lemma isOpen_Ico_zero {x : NNReal} : IsOpen (Set.Ico 0 x) :=
  Ico_bot (a := x) ‚ñ∏ isOpen_Iio

open Filter Finset

@[fun_prop]
theorem _root_.continuous_real_toNNReal : Continuous Real.toNNReal :=
  (continuous_id.max continuous_const).subtype_mk _

/-- `Real.toNNReal` bundled as a continuous map for convenience. -/
@[simps -fullyApplied]
noncomputable def _root_.ContinuousMap.realToNNReal : C(‚Ñù, ‚Ñù‚â•0) :=
  .mk Real.toNNReal continuous_real_toNNReal

lemma _root_.ContinuousOn.ofReal_map_toNNReal {f : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0} {s : Set ‚Ñù} {t : Set ‚Ñù‚â•0}
    (hf : ContinuousOn f t) (h : Set.MapsTo Real.toNNReal s t) :
    ContinuousOn (fun x ‚Ü¶ f x.toNNReal : ‚Ñù ‚Üí ‚Ñù) s :=
  continuous_subtype_val.comp_continuousOn <| hf.comp continuous_real_toNNReal.continuousOn h

@[simp, norm_cast]
theorem tendsto_coe {f : Filter Œ±} {m : Œ± ‚Üí ‚Ñù‚â•0} {x : ‚Ñù‚â•0} :
    Tendsto (fun a => (m a : ‚Ñù)) f (ùìù (x : ‚Ñù)) ‚Üî Tendsto m f (ùìù x) :=
  tendsto_subtype_rng.symm

theorem tendsto_coe' {f : Filter Œ±} [NeBot f] {m : Œ± ‚Üí ‚Ñù‚â•0} {x : ‚Ñù} :
    Tendsto (fun a => m a : Œ± ‚Üí ‚Ñù) f (ùìù x) ‚Üî ‚àÉ hx : 0 ‚â§ x, Tendsto m f (ùìù ‚ü®x, hx‚ü©) :=
  ‚ü®fun h => ‚ü®ge_of_tendsto' h fun c => (m c).2, tendsto_coe.1 h‚ü©, fun ‚ü®_, hm‚ü© => tendsto_coe.2 hm‚ü©

@[simp] theorem map_coe_atTop : map toReal atTop = atTop := map_val_Ici_atTop 0

@[simp]
theorem comap_coe_atTop : comap toReal atTop = atTop := (atTop_Ici_eq 0).symm

@[simp, norm_cast]
theorem tendsto_coe_atTop {f : Filter Œ±} {m : Œ± ‚Üí ‚Ñù‚â•0} :
    Tendsto (fun a => (m a : ‚Ñù)) f atTop ‚Üî Tendsto m f atTop :=
  tendsto_Ici_atTop.symm

theorem _root_.tendsto_real_toNNReal {f : Filter Œ±} {m : Œ± ‚Üí ‚Ñù} {x : ‚Ñù} (h : Tendsto m f (ùìù x)) :
    Tendsto (fun a => Real.toNNReal (m a)) f (ùìù (Real.toNNReal x)) :=
  (continuous_real_toNNReal.tendsto _).comp h

@[simp]
theorem _root_.Real.map_toNNReal_atTop : map Real.toNNReal atTop = atTop := by
  rw [‚Üê map_coe_atTop, Function.LeftInverse.filter_map @Real.toNNReal_coe]

theorem _root_.tendsto_real_toNNReal_atTop : Tendsto Real.toNNReal atTop atTop :=
  Real.map_toNNReal_atTop.le

@[simp]
theorem _root_.Real.comap_toNNReal_atTop : comap Real.toNNReal atTop = atTop := by
  refine le_antisymm ?_ tendsto_real_toNNReal_atTop.le_comap
  refine (atTop_basis_Ioi' 0).ge_iff.2 fun a ha ‚Ü¶ ?_
  filter_upwards [preimage_mem_comap (Ioi_mem_atTop a.toNNReal)] with x hx
  exact (Real.toNNReal_lt_toNNReal_iff_of_nonneg ha.le).1 hx

@[simp]
theorem _root_.Real.tendsto_toNNReal_atTop_iff {l : Filter Œ±} {f : Œ± ‚Üí ‚Ñù} :
    Tendsto (fun x ‚Ü¶ (f x).toNNReal) l atTop ‚Üî Tendsto f l atTop := by
  rw [‚Üê Real.comap_toNNReal_atTop, tendsto_comap_iff, Function.comp_def]

theorem _root_.Real.tendsto_toNNReal_atTop : Tendsto Real.toNNReal atTop atTop :=
  Real.tendsto_toNNReal_atTop_iff.2 tendsto_id

theorem nhds_zero : ùìù (0 : ‚Ñù‚â•0) = ‚®Ö (a : ‚Ñù‚â•0) (_ : a ‚â† 0), ùìü (Iio a) :=
  nhds_bot_order.trans <| by simp only [bot_lt_iff_ne_bot]; rfl

theorem nhds_zero_basis : (ùìù (0 : ‚Ñù‚â•0)).HasBasis (fun a : ‚Ñù‚â•0 => 0 < a) fun a => Iio a :=
  nhds_bot_basis


@[norm_cast]
theorem hasSum_coe {f : Œ± ‚Üí ‚Ñù‚â•0} {r : ‚Ñù‚â•0} :
    HasSum (fun a => (f a : ‚Ñù)) (r : ‚Ñù) L ‚Üî HasSum f r L := by
  simp only [HasSum, ‚Üê coe_sum, tendsto_coe]

protected theorem _root_.HasSum.toNNReal {f : Œ± ‚Üí ‚Ñù} {y : ‚Ñù} (hf‚ÇÄ : ‚àÄ n, 0 ‚â§ f n)
    (hy : HasSum f y L) : HasSum (fun x => Real.toNNReal (f x)) y.toNNReal L := by
  rcases L.neBot_or_eq_bot with _ | hL
  ¬∑ lift y to ‚Ñù‚â•0 using hy.nonneg hf‚ÇÄ
    lift f to Œ± ‚Üí ‚Ñù‚â•0 using hf‚ÇÄ
    simpa [hasSum_coe] using hy
  ¬∑ simp [HasSum, hL]

theorem hasSum_real_toNNReal_of_nonneg {f : Œ± ‚Üí ‚Ñù} (hf_nonneg : ‚àÄ n, 0 ‚â§ f n)
    (hf : Summable f L) :
    HasSum (fun n => Real.toNNReal (f n)) (Real.toNNReal (‚àë'[L] n, f n)) L :=
  hf.hasSum.toNNReal hf_nonneg

@[norm_cast]
theorem summable_coe {f : Œ± ‚Üí ‚Ñù‚â•0} :
    (Summable (fun a => (f a : ‚Ñù)) L) ‚Üî Summable f L := by
  rcases L.neBot_or_eq_bot with _ | hL
  ¬∑ constructor
    ¬∑ exact fun ‚ü®a, ha‚ü© => ‚ü®‚ü®a, ha.nonneg fun x => (f x).2‚ü©, hasSum_coe.1 ha‚ü©
    ¬∑ exact fun ‚ü®a, ha‚ü© => ‚ü®a.1, hasSum_coe.2 ha‚ü©
  ¬∑ simp [Summable, HasSum, hL]

theorem summable_mk {f : Œ± ‚Üí ‚Ñù} (hf : ‚àÄ n, 0 ‚â§ f n) :
    Summable (fun n ‚Ü¶ ‚ü®f n, hf n‚ü© : Œ± ‚Üí ‚Ñù‚â•0) L ‚Üî Summable f L :=
  Iff.symm <| summable_coe (f := fun x => ‚ü®f x, hf x‚ü©)

@[norm_cast]
theorem coe_tsum {f : Œ± ‚Üí ‚Ñù‚â•0} : ‚Üë(‚àë'[L] a, f a) = ‚àë'[L] a, (f a : ‚Ñù) :=
  Function.LeftInverse.map_tsum (g := NNReal.toRealHom)
    f NNReal.continuous_coe continuous_real_toNNReal (fun x ‚Ü¶ by simp)

theorem coe_tsum_of_nonneg {f : Œ± ‚Üí ‚Ñù} (hf‚ÇÅ : ‚àÄ n, 0 ‚â§ f n) :
    (‚ü®‚àë'[L] n, f n, tsum_nonneg hf‚ÇÅ‚ü© : ‚Ñù‚â•0) = (‚àë'[L] n, ‚ü®f n, hf‚ÇÅ n‚ü© : ‚Ñù‚â•0) :=
  NNReal.eq <| Eq.symm <| coe_tsum (f := fun x => ‚ü®f x, hf‚ÇÅ x‚ü©)

nonrec theorem tsum_mul_left (a : ‚Ñù‚â•0) (f : Œ± ‚Üí ‚Ñù‚â•0) :
    ‚àë'[L] x, a * f x = a * ‚àë'[L] x, f x :=
  NNReal.eq <| by simp only [coe_tsum, NNReal.coe_mul, tsum_mul_left]

nonrec theorem tsum_mul_right (f : Œ± ‚Üí ‚Ñù‚â•0) (a : ‚Ñù‚â•0) :
    ‚àë'[L] x, f x * a = (‚àë'[L] x, f x) * a :=
  NNReal.eq <| by simp only [coe_tsum, NNReal.coe_mul, tsum_mul_right]

theorem summable_comp_injective {Œ≤ : Type*} {f : Œ± ‚Üí ‚Ñù‚â•0} (hf : Summable f) {i : Œ≤ ‚Üí Œ±}
    (hi : Function.Injective i) : Summable (f ‚àò i) := by
  rw [‚Üê summable_coe] at hf ‚ä¢
  exact hf.comp_injective hi

theorem summable_nat_add (f : ‚Ñï ‚Üí ‚Ñù‚â•0) (hf : Summable f) (k : ‚Ñï) : Summable fun i => f (i + k) :=
  summable_comp_injective hf <| add_left_injective k

nonrec theorem summable_nat_add_iff {f : ‚Ñï ‚Üí ‚Ñù‚â•0} (k : ‚Ñï) :
    (Summable fun i => f (i + k)) ‚Üî Summable f := by
  rw [‚Üê summable_coe, ‚Üê summable_coe]
  exact @summable_nat_add_iff ‚Ñù _ _ _ (fun i => (f i : ‚Ñù)) k

nonrec theorem hasSum_nat_add_iff {f : ‚Ñï ‚Üí ‚Ñù‚â•0} (k : ‚Ñï) {a : ‚Ñù‚â•0} :
    HasSum (fun n => f (n + k)) a ‚Üî HasSum f (a + ‚àë i ‚àà range k, f i) := by
  rw [‚Üê hasSum_coe, hasSum_nat_add_iff (f := fun n => toReal (f n)) k]; norm_cast

theorem sum_add_tsum_nat_add {f : ‚Ñï ‚Üí ‚Ñù‚â•0} (k : ‚Ñï) (hf : Summable f) :
    ‚àë' i, f i = (‚àë i ‚àà range k, f i) + ‚àë' i, f (i + k) :=
  (((summable_nat_add_iff k).2 hf).sum_add_tsum_nat_add').symm

theorem iInf_real_pos_eq_iInf_nnreal_pos [CompleteLattice Œ±] {f : ‚Ñù ‚Üí Œ±} :
    ‚®Ö (n : ‚Ñù) (_ : 0 < n), f n = ‚®Ö (n : ‚Ñù‚â•0) (_ : 0 < n), f n :=
  le_antisymm (iInf_mono' fun r => ‚ü®r, le_rfl‚ü©) (iInf‚ÇÇ_mono' fun r hr => ‚ü®‚ü®r, hr.le‚ü©, hr, le_rfl‚ü©)

end coe

theorem tendsto_cofinite_zero_of_summable {Œ±} {f : Œ± ‚Üí ‚Ñù‚â•0} (hf : Summable f) :
    Tendsto f cofinite (ùìù 0) := by
  simp only [‚Üê summable_coe, ‚Üê tendsto_coe] at hf ‚ä¢
  exact hf.tendsto_cofinite_zero

theorem tendsto_atTop_zero_of_summable {f : ‚Ñï ‚Üí ‚Ñù‚â•0} (hf : Summable f) : Tendsto f atTop (ùìù 0) := by
  rw [‚Üê Nat.cofinite_eq_atTop]
  exact tendsto_cofinite_zero_of_summable hf

/-- The sum over the complement of a finset tends to `0` when the finset grows to cover the whole
space. This does not need a summability assumption, as otherwise all sums are zero. -/
nonrec theorem tendsto_tsum_compl_atTop_zero {Œ± : Type*} (f : Œ± ‚Üí ‚Ñù‚â•0) :
    Tendsto (fun s : Finset Œ± => ‚àë' b : { x // x ‚àâ s }, f b) atTop (ùìù 0) := by
  simp_rw [‚Üê tendsto_coe, coe_tsum, NNReal.coe_zero]
  exact tendsto_tsum_compl_atTop_zero fun a : Œ± => (f a : ‚Ñù)

/-- `x ‚Ü¶ x ^ n` as an order isomorphism of `‚Ñù‚â•0`. -/
def powOrderIso (n : ‚Ñï) (hn : n ‚â† 0) : ‚Ñù‚â•0 ‚âÉo ‚Ñù‚â•0 :=
  StrictMono.orderIsoOfSurjective (fun x ‚Ü¶ x ^ n) (fun x y h =>
      pow_left_strictMonoOn‚ÇÄ hn (zero_le x) (zero_le y) h) <|
    (continuous_id.pow _).surjective (tendsto_pow_atTop hn) <| by
      simpa [OrderBot.atBot_eq, pos_iff_ne_zero]

section Monotone

/-- A monotone, bounded above sequence `f : ‚Ñï ‚Üí ‚Ñù` has a finite limit. -/
theorem _root_.Real.tendsto_of_bddAbove_monotone {f : ‚Ñï ‚Üí ‚Ñù} (h_bdd : BddAbove (Set.range f))
    (h_mon : Monotone f) : ‚àÉ r : ‚Ñù, Tendsto f atTop (ùìù r) := by
  obtain ‚ü®B, hB‚ü© := Real.exists_isLUB (Set.range_nonempty f) h_bdd
  exact ‚ü®B, tendsto_atTop_isLUB h_mon hB‚ü©

/-- An antitone, bounded below sequence `f : ‚Ñï ‚Üí ‚Ñù` has a finite limit. -/
theorem _root_.Real.tendsto_of_bddBelow_antitone {f : ‚Ñï ‚Üí ‚Ñù} (h_bdd : BddBelow (Set.range f))
    (h_ant : Antitone f) : ‚àÉ r : ‚Ñù, Tendsto f atTop (ùìù r) := by
  obtain ‚ü®B, hB‚ü© := Real.exists_isGLB (Set.range_nonempty f) h_bdd
  exact ‚ü®B, tendsto_atTop_isGLB h_ant hB‚ü©

/-- An antitone sequence `f : ‚Ñï ‚Üí ‚Ñù‚â•0` has a finite limit. -/
theorem tendsto_of_antitone {f : ‚Ñï ‚Üí ‚Ñù‚â•0} (h_ant : Antitone f) :
    ‚àÉ r : ‚Ñù‚â•0, Tendsto f atTop (ùìù r) := by
  have h_bdd_0 : (0 : ‚Ñù) ‚àà lowerBounds (Set.range fun n : ‚Ñï => (f n : ‚Ñù)) := by
    rintro r ‚ü®n, hn‚ü©
    simp_rw [‚Üê hn]
    exact NNReal.coe_nonneg _
  obtain ‚ü®L, hL‚ü© := Real.tendsto_of_bddBelow_antitone ‚ü®0, h_bdd_0‚ü© h_ant
  have hL0 : 0 ‚â§ L :=
    haveI h_glb : IsGLB (Set.range fun n => (f n : ‚Ñù)) L := isGLB_of_tendsto_atTop h_ant hL
    (le_isGLB_iff h_glb).mpr h_bdd_0
  exact ‚ü®‚ü®L, hL0‚ü©, NNReal.tendsto_coe.mp hL‚ü©

end Monotone

lemma iSup_pow_of_ne_zero (hn : n ‚â† 0) (f : Œπ ‚Üí ‚Ñù‚â•0) : (‚®Ü i, f i) ^ n = ‚®Ü i, f i ^ n :=
  (NNReal.powOrderIso n hn).map_ciSup' _

lemma iSup_pow [Nonempty Œπ] (f : Œπ ‚Üí ‚Ñù‚â•0) (n : ‚Ñï) : (‚®Ü i, f i) ^ n = ‚®Ü i, f i ^ n := by
  by_cases hn : n = 0
  ¬∑ simp [hn]
  ¬∑ exact iSup_pow_of_ne_zero hn _

end NNReal

namespace ENNReal

attribute [simp] ENNReal.top_pow

/-- `x ‚Ü¶ x ^ n` as an order isomorphism of `‚Ñù‚â•0‚àû`.

See also `ENNReal.orderIsoRpow`. -/
def powOrderIso (n : ‚Ñï) (hn : n ‚â† 0) : ‚Ñù‚â•0‚àû ‚âÉo ‚Ñù‚â•0‚àû :=
  (NNReal.powOrderIso n hn).withTopCongr.copy (¬∑ ^ n) _
    (by cases n; (¬∑ cases hn rfl); ¬∑ ext (_ | _) <;> rfl) rfl

lemma iSup_pow_of_ne_zero (hn : n ‚â† 0) (f : Œπ ‚Üí ‚Ñù‚â•0‚àû) : (‚®Ü i, f i) ^ n = ‚®Ü i, f i ^ n :=
  (powOrderIso n hn).map_iSup _

lemma iSup_pow [Nonempty Œπ] (f : Œπ ‚Üí ‚Ñù‚â•0‚àû) (n : ‚Ñï) : (‚®Ü i, f i) ^ n = ‚®Ü i, f i ^ n := by
  by_cases hn : n = 0
  ¬∑ simp [hn]
  ¬∑ exact iSup_pow_of_ne_zero hn _

lemma iSup‚ÇÇ_pow_of_ne_zero {Œ∫ : Œπ ‚Üí Sort*} (f : (i : Œπ) ‚Üí Œ∫ i ‚Üí ‚Ñù‚â•0‚àû) {n : ‚Ñï} (hn : n ‚â† 0) :
    (‚®Ü i, ‚®Ü j, f i j) ^ n = ‚®Ü i, ‚®Ü j, f i j ^ n :=
  (powOrderIso n hn).map_iSup‚ÇÇ f

end ENNReal

open NNReal in
lemma Real.iSup_pow [Nonempty Œπ] {f : Œπ ‚Üí ‚Ñù} (hf : ‚àÄ i, 0 ‚â§ f i) (n : ‚Ñï) :
    (‚®Ü i, f i) ^ n = ‚®Ü i, f i ^ n := by
  lift f to Œπ ‚Üí ‚Ñù‚â•0 using hf; dsimp; exact mod_cast NNReal.iSup_pow f n

lemma Real.iSup_pow_of_ne_zero {f : Œπ ‚Üí ‚Ñù} (hf : ‚àÄ i, 0 ‚â§ f i) (hn : n ‚â† 0) :
    (‚®Ü i, f i) ^ n = ‚®Ü i, f i ^ n := by
  cases isEmpty_or_nonempty Œπ
  ¬∑ simp [hn]
  ¬∑ exact iSup_pow hf _
