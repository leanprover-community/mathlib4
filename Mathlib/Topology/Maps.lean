/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Patrick Massot
-/
import Mathlib.Topology.Order
import Mathlib.Topology.NhdsSet

#align_import topology.maps from "leanprover-community/mathlib"@"d91e7f7a7f1c7e9f0e18fdb6bde4f652004c735d"

/-!
# Specific classes of maps between topological spaces

This file introduces the following properties of a map `f : X â†’ Y` between topological spaces:

* `IsOpenMap f` means the image of an open set under `f` is open.
* `IsClosedMap f` means the image of a closed set under `f` is closed.

(Open and closed maps need not be continuous.)

* `Inducing f` means the topology on `X` is the one induced via `f` from the topology on `Y`.
  These behave like embeddings except they need not be injective. Instead, points of `X` which
  are identified by `f` are also inseparable in the topology on `X`.
* `Embedding f` means `f` is inducing and also injective. Equivalently, `f` identifies `X` with
  a subspace of `Y`.
* `OpenEmbedding f` means `f` is an embedding with open image, so it identifies `X` with an
  open subspace of `Y`. Equivalently, `f` is an embedding and an open map.
* `ClosedEmbedding f` similarly means `f` is an embedding with closed image, so it identifies
  `X` with a closed subspace of `Y`. Equivalently, `f` is an embedding and a closed map.

* `QuotientMap f` is the dual condition to `Embedding f`: `f` is surjective and the topology
  on `Y` is the one coinduced via `f` from the topology on `X`. Equivalently, `f` identifies
  `Y` with a quotient of `X`. Quotient maps are also sometimes known as identification maps.

## References

* <https://en.wikipedia.org/wiki/Open_and_closed_maps>
* <https://en.wikipedia.org/wiki/Embedding#General_topology>
* <https://en.wikipedia.org/wiki/Quotient_space_(topology)#Quotient_map>

## Tags

open map, closed map, embedding, quotient map, identification map

-/


open Set Filter Function

open TopologicalSpace Topology Filter

variable {Î± : Type*} {Î² : Type*} {Î³ : Type*} {Î´ : Type*}

section Inducing

/-- A function `f : Î± â†’ Î²` between topological spaces is inducing if the topology on `Î±` is induced
by the topology on `Î²` through `f`, meaning that a set `s : Set Î±` is open iff it is the preimage
under `f` of some open set `t : Set Î²`. -/
@[mk_iff inducing_iff]
structure Inducing [tÎ± : TopologicalSpace Î±] [tÎ² : TopologicalSpace Î²] (f : Î± â†’ Î²) : Prop where
  /-- The topology on the domain is equal to the induced topology. -/
  induced : tÎ± = tÎ².induced f
#align inducing Inducing
#align inducing_iff inducing_iff

variable [TopologicalSpace Î±] [TopologicalSpace Î²] [TopologicalSpace Î³] [TopologicalSpace Î´]

theorem inducing_induced (f : Î± â†’ Î²) : @Inducing Î± Î² (TopologicalSpace.induced f â€¹_â€º) _ f :=
  @Inducing.mk _ _ (TopologicalSpace.induced f â€¹_â€º) _ _ rfl

theorem inducing_id : Inducing (@id Î±) :=
  âŸ¨induced_id.symmâŸ©
#align inducing_id inducing_id

protected theorem Inducing.comp {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : Inducing g) (hf : Inducing f) :
    Inducing (g âˆ˜ f) :=
  âŸ¨by rw [hf.induced, hg.induced, induced_compose]âŸ©
      -- ğŸ‰ no goals
#align inducing.comp Inducing.comp

theorem inducing_of_inducing_compose {f : Î± â†’ Î²} {g : Î² â†’ Î³} (hf : Continuous f) (hg : Continuous g)
    (hgf : Inducing (g âˆ˜ f)) : Inducing f :=
  âŸ¨le_antisymm (by rwa [â† continuous_iff_le_induced])
                   -- ğŸ‰ no goals
      (by
        rw [hgf.induced, â† induced_compose]
        -- âŠ¢ induced f instâœÂ² â‰¤ induced f (induced g instâœÂ¹)
        exact induced_mono hg.le_induced)âŸ©
        -- ğŸ‰ no goals
#align inducing_of_inducing_compose inducing_of_inducing_compose

theorem inducing_iff_nhds {f : Î± â†’ Î²} : Inducing f â†” âˆ€ a, ğ“ a = comap f (ğ“ (f a)) :=
  (inducing_iff _).trans (induced_iff_nhds_eq f)
#align inducing_iff_nhds inducing_iff_nhds

theorem Inducing.nhds_eq_comap {f : Î± â†’ Î²} (hf : Inducing f) : âˆ€ a : Î±, ğ“ a = comap f (ğ“ <| f a) :=
  inducing_iff_nhds.1 hf
#align inducing.nhds_eq_comap Inducing.nhds_eq_comap

theorem Inducing.nhdsSet_eq_comap {f : Î± â†’ Î²} (hf : Inducing f) (s : Set Î±) :
    ğ“Ë¢ s = comap f (ğ“Ë¢ (f '' s)) := by
  simp only [nhdsSet, sSup_image, comap_iSup, hf.nhds_eq_comap, iSup_image]
  -- ğŸ‰ no goals
#align inducing.nhds_set_eq_comap Inducing.nhdsSet_eq_comap

theorem Inducing.map_nhds_eq {f : Î± â†’ Î²} (hf : Inducing f) (a : Î±) : (ğ“ a).map f = ğ“[range f] f a :=
  hf.induced.symm â–¸ map_nhds_induced_eq a
#align inducing.map_nhds_eq Inducing.map_nhds_eq

theorem Inducing.map_nhds_of_mem {f : Î± â†’ Î²} (hf : Inducing f) (a : Î±) (h : range f âˆˆ ğ“ (f a)) :
    (ğ“ a).map f = ğ“ (f a) :=
  hf.induced.symm â–¸ map_nhds_induced_of_mem h
#align inducing.map_nhds_of_mem Inducing.map_nhds_of_mem

-- porting note: new lemma
theorem Inducing.mapClusterPt_iff {f : Î± â†’ Î²} (hf : Inducing f) {a : Î±} {l : Filter Î±} :
    MapClusterPt (f a) l f â†” ClusterPt a l := by
  delta MapClusterPt ClusterPt
  -- âŠ¢ NeBot (ğ“ (f a) âŠ“ map f l) â†” NeBot (ğ“ a âŠ“ l)
  rw [â† Filter.push_pull', â† hf.nhds_eq_comap, map_neBot_iff]
  -- ğŸ‰ no goals

theorem Inducing.image_mem_nhdsWithin {f : Î± â†’ Î²} (hf : Inducing f) {a : Î±} {s : Set Î±}
    (hs : s âˆˆ ğ“ a) : f '' s âˆˆ ğ“[range f] f a :=
  hf.map_nhds_eq a â–¸ image_mem_map hs
#align inducing.image_mem_nhds_within Inducing.image_mem_nhdsWithin

theorem Inducing.tendsto_nhds_iff {Î¹ : Type*} {f : Î¹ â†’ Î²} {g : Î² â†’ Î³} {a : Filter Î¹} {b : Î²}
    (hg : Inducing g) : Tendsto f a (ğ“ b) â†” Tendsto (g âˆ˜ f) a (ğ“ (g b)) := by
  rw [hg.nhds_eq_comap, tendsto_comap_iff]
  -- ğŸ‰ no goals
#align inducing.tendsto_nhds_iff Inducing.tendsto_nhds_iff

theorem Inducing.continuousAt_iff {f : Î± â†’ Î²} {g : Î² â†’ Î³} (hg : Inducing g) {x : Î±} :
    ContinuousAt f x â†” ContinuousAt (g âˆ˜ f) x :=
  hg.tendsto_nhds_iff
#align inducing.continuous_at_iff Inducing.continuousAt_iff

theorem Inducing.continuous_iff {f : Î± â†’ Î²} {g : Î² â†’ Î³} (hg : Inducing g) :
    Continuous f â†” Continuous (g âˆ˜ f) := by
  simp_rw [continuous_iff_continuousAt, hg.continuousAt_iff]
  -- ğŸ‰ no goals
#align inducing.continuous_iff Inducing.continuous_iff

theorem Inducing.continuousAt_iff' {f : Î± â†’ Î²} {g : Î² â†’ Î³} (hf : Inducing f) {x : Î±}
    (h : range f âˆˆ ğ“ (f x)) : ContinuousAt (g âˆ˜ f) x â†” ContinuousAt g (f x) := by
  simp_rw [ContinuousAt, Filter.Tendsto, â† hf.map_nhds_of_mem _ h, Filter.map_map, comp]
  -- ğŸ‰ no goals
#align inducing.continuous_at_iff' Inducing.continuousAt_iff'

protected theorem Inducing.continuous {f : Î± â†’ Î²} (hf : Inducing f) : Continuous f :=
  hf.continuous_iff.mp continuous_id
#align inducing.continuous Inducing.continuous

protected theorem Inducing.inducing_iff {f : Î± â†’ Î²} {g : Î² â†’ Î³} (hg : Inducing g) :
    Inducing f â†” Inducing (g âˆ˜ f) := by
  refine' âŸ¨fun h => hg.comp h, fun hgf => inducing_of_inducing_compose _ hg.continuous hgfâŸ©
  -- âŠ¢ Continuous f
  rw [hg.continuous_iff]
  -- âŠ¢ Continuous (g âˆ˜ f)
  exact hgf.continuous
  -- ğŸ‰ no goals
#align inducing.inducing_iff Inducing.inducing_iff

theorem Inducing.closure_eq_preimage_closure_image {f : Î± â†’ Î²} (hf : Inducing f) (s : Set Î±) :
    closure s = f â»Â¹' closure (f '' s) := by
  ext x
  -- âŠ¢ x âˆˆ closure s â†” x âˆˆ f â»Â¹' closure (f '' s)
  rw [Set.mem_preimage, â† closure_induced, hf.induced]
  -- ğŸ‰ no goals
#align inducing.closure_eq_preimage_closure_image Inducing.closure_eq_preimage_closure_image

theorem Inducing.isClosed_iff {f : Î± â†’ Î²} (hf : Inducing f) {s : Set Î±} :
    IsClosed s â†” âˆƒ t, IsClosed t âˆ§ f â»Â¹' t = s := by rw [hf.induced, isClosed_induced_iff]
                                                     -- ğŸ‰ no goals
#align inducing.is_closed_iff Inducing.isClosed_iff

theorem Inducing.isClosed_iff' {f : Î± â†’ Î²} (hf : Inducing f) {s : Set Î±} :
    IsClosed s â†” âˆ€ x, f x âˆˆ closure (f '' s) â†’ x âˆˆ s := by rw [hf.induced, isClosed_induced_iff']
                                                           -- ğŸ‰ no goals
#align inducing.is_closed_iff' Inducing.isClosed_iff'

theorem Inducing.isClosed_preimage {f : Î± â†’ Î²} (h : Inducing f) (s : Set Î²) (hs : IsClosed s) :
    IsClosed (f â»Â¹' s) :=
  (Inducing.isClosed_iff h).mpr âŸ¨s, hs, rflâŸ©
#align inducing.is_closed_preimage Inducing.isClosed_preimage

theorem Inducing.isOpen_iff {f : Î± â†’ Î²} (hf : Inducing f) {s : Set Î±} :
    IsOpen s â†” âˆƒ t, IsOpen t âˆ§ f â»Â¹' t = s := by rw [hf.induced, isOpen_induced_iff]
                                                 -- ğŸ‰ no goals
#align inducing.is_open_iff Inducing.isOpen_iff

theorem Inducing.setOf_isOpen {f : Î± â†’ Î²} (hf : Inducing f) :
    {s : Set Î± | IsOpen s} = preimage f '' {t | IsOpen t} :=
  Set.ext fun _ â†¦ hf.isOpen_iff

theorem Inducing.dense_iff {f : Î± â†’ Î²} (hf : Inducing f) {s : Set Î±} :
    Dense s â†” âˆ€ x, f x âˆˆ closure (f '' s) := by
  simp only [Dense, hf.closure_eq_preimage_closure_image, mem_preimage]
  -- ğŸ‰ no goals
#align inducing.dense_iff Inducing.dense_iff

end Inducing

section Embedding

/-- A function between topological spaces is an embedding if it is injective,
  and for all `s : Set Î±`, `s` is open iff it is the preimage of an open set. -/
@[mk_iff embedding_iff]
structure Embedding [TopologicalSpace Î±] [TopologicalSpace Î²] (f : Î± â†’ Î²) extends
  Inducing f : Prop where
  /-- A topological embedding is injective. -/
  inj : Injective f
#align embedding Embedding
#align embedding_iff embedding_iff

theorem Function.Injective.embedding_induced [t : TopologicalSpace Î²] {f : Î± â†’ Î²}
    (hf : Injective f) : @_root_.Embedding Î± Î² (t.induced f) t f :=
  @_root_.Embedding.mk Î± Î² (t.induced f) t _ (inducing_induced f) hf
#align function.injective.embedding_induced Function.Injective.embedding_induced

variable [TopologicalSpace Î±] [TopologicalSpace Î²] [TopologicalSpace Î³]

theorem Embedding.mk' (f : Î± â†’ Î²) (inj : Injective f) (induced : âˆ€ a, comap f (ğ“ (f a)) = ğ“ a) :
    Embedding f :=
  âŸ¨inducing_iff_nhds.2 fun a => (induced a).symm, injâŸ©
#align embedding.mk' Embedding.mk'

theorem embedding_id : Embedding (@id Î±) :=
  âŸ¨inducing_id, fun _ _ h => hâŸ©
#align embedding_id embedding_id

protected theorem Embedding.comp {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : Embedding g) (hf : Embedding f) :
    Embedding (g âˆ˜ f) :=
  { hg.toInducing.comp hf.toInducing with inj := fun _ _ h => hf.inj <| hg.inj h }
#align embedding.comp Embedding.comp

theorem embedding_of_embedding_compose {f : Î± â†’ Î²} {g : Î² â†’ Î³} (hf : Continuous f)
    (hg : Continuous g) (hgf : Embedding (g âˆ˜ f)) : Embedding f :=
  { induced := (inducing_of_inducing_compose hf hg hgf.toInducing).induced
    inj := fun aâ‚ aâ‚‚ h => hgf.inj <| by simp [h, (Â· âˆ˜ Â·)] }
                                        -- ğŸ‰ no goals
#align embedding_of_embedding_compose embedding_of_embedding_compose

protected theorem Function.LeftInverse.embedding {f : Î± â†’ Î²} {g : Î² â†’ Î±} (h : LeftInverse f g)
    (hf : Continuous f) (hg : Continuous g) : _root_.Embedding g :=
  embedding_of_embedding_compose hg hf <| h.comp_eq_id.symm â–¸ embedding_id
#align function.left_inverse.embedding Function.LeftInverse.embedding

theorem Embedding.map_nhds_eq {f : Î± â†’ Î²} (hf : Embedding f) (a : Î±) :
    (ğ“ a).map f = ğ“[range f] f a :=
  hf.1.map_nhds_eq a
#align embedding.map_nhds_eq Embedding.map_nhds_eq

theorem Embedding.map_nhds_of_mem {f : Î± â†’ Î²} (hf : Embedding f) (a : Î±) (h : range f âˆˆ ğ“ (f a)) :
    (ğ“ a).map f = ğ“ (f a) :=
  hf.1.map_nhds_of_mem a h
#align embedding.map_nhds_of_mem Embedding.map_nhds_of_mem

theorem Embedding.tendsto_nhds_iff {Î¹ : Type*} {f : Î¹ â†’ Î²} {g : Î² â†’ Î³} {a : Filter Î¹} {b : Î²}
    (hg : Embedding g) : Tendsto f a (ğ“ b) â†” Tendsto (g âˆ˜ f) a (ğ“ (g b)) :=
  hg.toInducing.tendsto_nhds_iff
#align embedding.tendsto_nhds_iff Embedding.tendsto_nhds_iff

theorem Embedding.continuous_iff {f : Î± â†’ Î²} {g : Î² â†’ Î³} (hg : Embedding g) :
    Continuous f â†” Continuous (g âˆ˜ f) :=
  Inducing.continuous_iff hg.1
#align embedding.continuous_iff Embedding.continuous_iff

theorem Embedding.continuous {f : Î± â†’ Î²} (hf : Embedding f) : Continuous f :=
  Inducing.continuous hf.1
#align embedding.continuous Embedding.continuous

theorem Embedding.closure_eq_preimage_closure_image {e : Î± â†’ Î²} (he : Embedding e) (s : Set Î±) :
    closure s = e â»Â¹' closure (e '' s) :=
  he.1.closure_eq_preimage_closure_image s
#align embedding.closure_eq_preimage_closure_image Embedding.closure_eq_preimage_closure_image

/-- The topology induced under an inclusion `f : X â†’ Y` from the discrete topological space `Y`
is the discrete topology on `X`. -/
theorem Embedding.discreteTopology {X Y : Type*} [TopologicalSpace X] [tY : TopologicalSpace Y]
    [DiscreteTopology Y] {f : X â†’ Y} (hf : Embedding f) : DiscreteTopology X :=
  discreteTopology_iff_nhds.2 fun x => by
    rw [hf.nhds_eq_comap, nhds_discrete, comap_pure, â† image_singleton, hf.inj.preimage_image,
      principal_singleton]
#align embedding.discrete_topology Embedding.discreteTopology

end Embedding

/-- A function between topological spaces is a quotient map if it is surjective,
  and for all `s : Set Î²`, `s` is open iff its preimage is an open set. -/
def QuotientMap {Î± : Type*} {Î² : Type*} [tÎ± : TopologicalSpace Î±] [tÎ² : TopologicalSpace Î²]
    (f : Î± â†’ Î²) : Prop :=
  Surjective f âˆ§ tÎ² = tÎ±.coinduced f
#align quotient_map QuotientMap

theorem quotientMap_iff [TopologicalSpace Î±] [TopologicalSpace Î²] {f : Î± â†’ Î²} :
    QuotientMap f â†” Surjective f âˆ§ âˆ€ s : Set Î², IsOpen s â†” IsOpen (f â»Â¹' s) :=
  and_congr Iff.rfl TopologicalSpace.ext_iff
#align quotient_map_iff quotientMap_iff

theorem quotientMap_iff_closed [TopologicalSpace Î±] [TopologicalSpace Î²] {f : Î± â†’ Î²} :
    QuotientMap f â†” Surjective f âˆ§ âˆ€ s : Set Î², IsClosed s â†” IsClosed (f â»Â¹' s) :=
  quotientMap_iff.trans <| Iff.rfl.and <| compl_surjective.forall.trans <| by
    simp only [isOpen_compl_iff, preimage_compl]
    -- ğŸ‰ no goals
#align quotient_map_iff_closed quotientMap_iff_closed

namespace QuotientMap

variable [TopologicalSpace Î±] [TopologicalSpace Î²] [TopologicalSpace Î³] [TopologicalSpace Î´]
  {g : Î² â†’ Î³} {f : Î± â†’ Î²}

protected theorem id : QuotientMap (@id Î±) :=
  âŸ¨fun a => âŸ¨a, rflâŸ©, coinduced_id.symmâŸ©
#align quotient_map.id QuotientMap.id

protected theorem comp (hg : QuotientMap g) (hf : QuotientMap f) : QuotientMap (g âˆ˜ f) :=
  âŸ¨hg.left.comp hf.left, by rw [hg.right, hf.right, coinduced_compose]âŸ©
                            -- ğŸ‰ no goals
#align quotient_map.comp QuotientMap.comp

protected theorem of_quotientMap_compose (hf : Continuous f) (hg : Continuous g)
    (hgf : QuotientMap (g âˆ˜ f)) : QuotientMap g :=
  âŸ¨hgf.1.of_comp,
    le_antisymm
      (by rw [hgf.right, â† coinduced_compose]; exact coinduced_mono hf.coinduced_le)
          -- âŠ¢ coinduced g (coinduced f instâœÂ³) â‰¤ coinduced g instâœÂ²
                                               -- ğŸ‰ no goals
      hg.coinduced_leâŸ©
#align quotient_map.of_quotient_map_compose QuotientMap.of_quotientMap_compose

theorem of_inverse {g : Î² â†’ Î±} (hf : Continuous f) (hg : Continuous g) (h : LeftInverse g f) :
    QuotientMap g :=
  QuotientMap.of_quotientMap_compose hf hg <| h.comp_eq_id.symm â–¸ QuotientMap.id
#align quotient_map.of_inverse QuotientMap.of_inverse

protected theorem continuous_iff (hf : QuotientMap f) : Continuous g â†” Continuous (g âˆ˜ f) := by
  rw [continuous_iff_coinduced_le, continuous_iff_coinduced_le, hf.right, coinduced_compose]
  -- ğŸ‰ no goals
#align quotient_map.continuous_iff QuotientMap.continuous_iff

protected theorem continuous (hf : QuotientMap f) : Continuous f :=
  hf.continuous_iff.mp continuous_id
#align quotient_map.continuous QuotientMap.continuous

protected theorem surjective (hf : QuotientMap f) : Surjective f :=
  hf.1
#align quotient_map.surjective QuotientMap.surjective

protected theorem isOpen_preimage (hf : QuotientMap f) {s : Set Î²} : IsOpen (f â»Â¹' s) â†” IsOpen s :=
  ((quotientMap_iff.1 hf).2 s).symm
#align quotient_map.is_open_preimage QuotientMap.isOpen_preimage

protected theorem isClosed_preimage (hf : QuotientMap f) {s : Set Î²} :
    IsClosed (f â»Â¹' s) â†” IsClosed s :=
  ((quotientMap_iff_closed.1 hf).2 s).symm
#align quotient_map.is_closed_preimage QuotientMap.isClosed_preimage

end QuotientMap

/-- A map `f : Î± â†’ Î²` is said to be an *open map*, if the image of any open `U : Set Î±`
is open in `Î²`. -/
def IsOpenMap [TopologicalSpace Î±] [TopologicalSpace Î²] (f : Î± â†’ Î²) :=
  âˆ€ U : Set Î±, IsOpen U â†’ IsOpen (f '' U)
#align is_open_map IsOpenMap

namespace IsOpenMap

variable [TopologicalSpace Î±] [TopologicalSpace Î²] [TopologicalSpace Î³] {f : Î± â†’ Î²}

protected theorem id : IsOpenMap (@id Î±) := fun s hs => by rwa [image_id]
                                                           -- ğŸ‰ no goals
#align is_open_map.id IsOpenMap.id

protected theorem comp {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : IsOpenMap g) (hf : IsOpenMap f) :
    IsOpenMap (g âˆ˜ f) := fun s hs => by rw [image_comp]; exact hg _ (hf _ hs)
                                        -- âŠ¢ IsOpen (g '' (f '' s))
                                                         -- ğŸ‰ no goals
#align is_open_map.comp IsOpenMap.comp

theorem isOpen_range (hf : IsOpenMap f) : IsOpen (range f) := by
  rw [â† image_univ]
  -- âŠ¢ IsOpen (f '' univ)
  exact hf _ isOpen_univ
  -- ğŸ‰ no goals
#align is_open_map.is_open_range IsOpenMap.isOpen_range

theorem image_mem_nhds (hf : IsOpenMap f) {x : Î±} {s : Set Î±} (hx : s âˆˆ ğ“ x) : f '' s âˆˆ ğ“ (f x) :=
  let âŸ¨t, hts, ht, hxtâŸ© := mem_nhds_iff.1 hx
  mem_of_superset (IsOpen.mem_nhds (hf t ht) (mem_image_of_mem _ hxt)) (image_subset _ hts)
#align is_open_map.image_mem_nhds IsOpenMap.image_mem_nhds

theorem range_mem_nhds (hf : IsOpenMap f) (x : Î±) : range f âˆˆ ğ“ (f x) :=
  hf.isOpen_range.mem_nhds <| mem_range_self _
#align is_open_map.range_mem_nhds IsOpenMap.range_mem_nhds

theorem mapsTo_interior (hf : IsOpenMap f) {s : Set Î±} {t : Set Î²} (h : MapsTo f s t) :
    MapsTo f (interior s) (interior t) :=
  mapsTo'.2 <|
    interior_maximal (h.mono interior_subset Subset.rfl).image_subset (hf _ isOpen_interior)
#align is_open_map.maps_to_interior IsOpenMap.mapsTo_interior

theorem image_interior_subset (hf : IsOpenMap f) (s : Set Î±) :
    f '' interior s âŠ† interior (f '' s) :=
  (hf.mapsTo_interior (mapsTo_image f s)).image_subset
#align is_open_map.image_interior_subset IsOpenMap.image_interior_subset

theorem nhds_le (hf : IsOpenMap f) (a : Î±) : ğ“ (f a) â‰¤ (ğ“ a).map f :=
  le_map fun _ => hf.image_mem_nhds
#align is_open_map.nhds_le IsOpenMap.nhds_le

theorem of_nhds_le (hf : âˆ€ a, ğ“ (f a) â‰¤ map f (ğ“ a)) : IsOpenMap f := fun _s hs =>
  isOpen_iff_mem_nhds.2 fun _b âŸ¨_a, has, habâŸ© => hab â–¸ hf _ (image_mem_map <| hs.mem_nhds has)
#align is_open_map.of_nhds_le IsOpenMap.of_nhds_le

theorem of_sections {f : Î± â†’ Î²}
    (h : âˆ€ x, âˆƒ g : Î² â†’ Î±, ContinuousAt g (f x) âˆ§ g (f x) = x âˆ§ RightInverse g f) : IsOpenMap f :=
  of_nhds_le fun x =>
    let âŸ¨g, hgc, hgx, hgfâŸ© := h x
    calc
      ğ“ (f x) = map f (map g (ğ“ (f x))) := by rw [map_map, hgf.comp_eq_id, map_id]
                                              -- ğŸ‰ no goals
      _ â‰¤ map f (ğ“ (g (f x))) := map_mono hgc
      _ = map f (ğ“ x) := by rw [hgx]
                            -- ğŸ‰ no goals
#align is_open_map.of_sections IsOpenMap.of_sections

theorem of_inverse {f : Î± â†’ Î²} {f' : Î² â†’ Î±} (h : Continuous f') (l_inv : LeftInverse f f')
    (r_inv : RightInverse f f') : IsOpenMap f :=
  of_sections fun _ => âŸ¨f', h.continuousAt, r_inv _, l_invâŸ©
#align is_open_map.of_inverse IsOpenMap.of_inverse

/-- A continuous surjective open map is a quotient map. -/
theorem to_quotientMap {f : Î± â†’ Î²} (open_map : IsOpenMap f) (cont : Continuous f)
    (surj : Surjective f) : QuotientMap f :=
  quotientMap_iff.2
    âŸ¨surj, fun s => âŸ¨fun h => h.preimage cont, fun h => surj.image_preimage s â–¸ open_map _ hâŸ©âŸ©
#align is_open_map.to_quotient_map IsOpenMap.to_quotientMap

theorem interior_preimage_subset_preimage_interior (hf : IsOpenMap f) {s : Set Î²} :
    interior (f â»Â¹' s) âŠ† f â»Â¹' interior s :=
  hf.mapsTo_interior (mapsTo_preimage _ _)
#align is_open_map.interior_preimage_subset_preimage_interior IsOpenMap.interior_preimage_subset_preimage_interior

theorem preimage_interior_eq_interior_preimage (hfâ‚ : IsOpenMap f) (hfâ‚‚ : Continuous f)
    (s : Set Î²) : f â»Â¹' interior s = interior (f â»Â¹' s) :=
  Subset.antisymm (preimage_interior_subset_interior_preimage hfâ‚‚)
    (interior_preimage_subset_preimage_interior hfâ‚)
#align is_open_map.preimage_interior_eq_interior_preimage IsOpenMap.preimage_interior_eq_interior_preimage

theorem preimage_closure_subset_closure_preimage (hf : IsOpenMap f) {s : Set Î²} :
    f â»Â¹' closure s âŠ† closure (f â»Â¹' s) := by
  rw [â† compl_subset_compl]
  -- âŠ¢ (closure (f â»Â¹' s))á¶œ âŠ† (f â»Â¹' closure s)á¶œ
  simp only [â† interior_compl, â† preimage_compl, hf.interior_preimage_subset_preimage_interior]
  -- ğŸ‰ no goals
#align is_open_map.preimage_closure_subset_closure_preimage IsOpenMap.preimage_closure_subset_closure_preimage

theorem preimage_closure_eq_closure_preimage (hf : IsOpenMap f) (hfc : Continuous f) (s : Set Î²) :
    f â»Â¹' closure s = closure (f â»Â¹' s) :=
  hf.preimage_closure_subset_closure_preimage.antisymm (hfc.closure_preimage_subset s)
#align is_open_map.preimage_closure_eq_closure_preimage IsOpenMap.preimage_closure_eq_closure_preimage

theorem preimage_frontier_subset_frontier_preimage (hf : IsOpenMap f) {s : Set Î²} :
    f â»Â¹' frontier s âŠ† frontier (f â»Â¹' s) := by
  simpa only [frontier_eq_closure_inter_closure, preimage_inter] using
    inter_subset_inter hf.preimage_closure_subset_closure_preimage
      hf.preimage_closure_subset_closure_preimage
#align is_open_map.preimage_frontier_subset_frontier_preimage IsOpenMap.preimage_frontier_subset_frontier_preimage

theorem preimage_frontier_eq_frontier_preimage (hf : IsOpenMap f) (hfc : Continuous f) (s : Set Î²) :
    f â»Â¹' frontier s = frontier (f â»Â¹' s) := by
  simp only [frontier_eq_closure_inter_closure, preimage_inter, preimage_compl,
    hf.preimage_closure_eq_closure_preimage hfc]
#align is_open_map.preimage_frontier_eq_frontier_preimage IsOpenMap.preimage_frontier_eq_frontier_preimage

end IsOpenMap

theorem isOpenMap_iff_nhds_le [TopologicalSpace Î±] [TopologicalSpace Î²] {f : Î± â†’ Î²} :
    IsOpenMap f â†” âˆ€ a : Î±, ğ“ (f a) â‰¤ (ğ“ a).map f :=
  âŸ¨fun hf => hf.nhds_le, IsOpenMap.of_nhds_leâŸ©
#align is_open_map_iff_nhds_le isOpenMap_iff_nhds_le

theorem isOpenMap_iff_interior [TopologicalSpace Î±] [TopologicalSpace Î²] {f : Î± â†’ Î²} :
    IsOpenMap f â†” âˆ€ s, f '' interior s âŠ† interior (f '' s) :=
  âŸ¨IsOpenMap.image_interior_subset, fun hs u hu =>
    subset_interior_iff_isOpen.mp <|
      calc
        f '' u = f '' interior u := by rw [hu.interior_eq]
                                       -- ğŸ‰ no goals
        _ âŠ† interior (f '' u) := hs uâŸ©
#align is_open_map_iff_interior isOpenMap_iff_interior

/-- An inducing map with an open range is an open map. -/
protected theorem Inducing.isOpenMap [TopologicalSpace Î±] [TopologicalSpace Î²] {f : Î± â†’ Î²}
    (hi : Inducing f) (ho : IsOpen (range f)) : IsOpenMap f :=
  IsOpenMap.of_nhds_le fun _ => (hi.map_nhds_of_mem _ <| IsOpen.mem_nhds ho <| mem_range_self _).ge
#align inducing.is_open_map Inducing.isOpenMap

section IsClosedMap

variable [TopologicalSpace Î±] [TopologicalSpace Î²]

/-- A map `f : Î± â†’ Î²` is said to be a *closed map*, if the image of any closed `U : Set Î±`
is closed in `Î²`. -/
def IsClosedMap (f : Î± â†’ Î²) :=
  âˆ€ U : Set Î±, IsClosed U â†’ IsClosed (f '' U)
#align is_closed_map IsClosedMap

end IsClosedMap

namespace IsClosedMap

variable [TopologicalSpace Î±] [TopologicalSpace Î²] [TopologicalSpace Î³]

open Function

protected theorem id : IsClosedMap (@id Î±) := fun s hs => by rwa [image_id]
                                                             -- ğŸ‰ no goals
#align is_closed_map.id IsClosedMap.id

protected theorem comp {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : IsClosedMap g) (hf : IsClosedMap f) :
    IsClosedMap (g âˆ˜ f) := by
  intro s hs
  -- âŠ¢ IsClosed (g âˆ˜ f '' s)
  rw [image_comp]
  -- âŠ¢ IsClosed (g '' (f '' s))
  exact hg _ (hf _ hs)
  -- ğŸ‰ no goals
#align is_closed_map.comp IsClosedMap.comp

theorem closure_image_subset {f : Î± â†’ Î²} (hf : IsClosedMap f) (s : Set Î±) :
    closure (f '' s) âŠ† f '' closure s :=
  closure_minimal (image_subset _ subset_closure) (hf _ isClosed_closure)
#align is_closed_map.closure_image_subset IsClosedMap.closure_image_subset

theorem of_inverse {f : Î± â†’ Î²} {f' : Î² â†’ Î±} (h : Continuous f') (l_inv : LeftInverse f f')
    (r_inv : RightInverse f f') : IsClosedMap f := fun s hs => by
  rw [image_eq_preimage_of_inverse r_inv l_inv]
  -- âŠ¢ IsClosed (f' â»Â¹' s)
  exact hs.preimage h
  -- ğŸ‰ no goals
#align is_closed_map.of_inverse IsClosedMap.of_inverse

theorem of_nonempty {f : Î± â†’ Î²} (h : âˆ€ s, IsClosed s â†’ s.Nonempty â†’ IsClosed (f '' s)) :
    IsClosedMap f := by
  intro s hs; cases' eq_empty_or_nonempty s with h2s h2s
  -- âŠ¢ IsClosed (f '' s)
              -- âŠ¢ IsClosed (f '' s)
  Â· simp_rw [h2s, image_empty, isClosed_empty]
    -- ğŸ‰ no goals
  Â· exact h s hs h2s
    -- ğŸ‰ no goals
#align is_closed_map.of_nonempty IsClosedMap.of_nonempty

theorem closed_range {f : Î± â†’ Î²} (hf : IsClosedMap f) : IsClosed (range f) :=
  @image_univ _ _ f â–¸ hf _ isClosed_univ
#align is_closed_map.closed_range IsClosedMap.closed_range

theorem to_quotientMap {f : Î± â†’ Î²} (hcl : IsClosedMap f) (hcont : Continuous f)
    (hsurj : Surjective f) : QuotientMap f :=
  quotientMap_iff_closed.2 âŸ¨hsurj, fun s =>
    âŸ¨fun hs => hs.preimage hcont, fun hs => hsurj.image_preimage s â–¸ hcl _ hsâŸ©âŸ©
#align is_closed_map.to_quotient_map IsClosedMap.to_quotientMap

end IsClosedMap

theorem Inducing.isClosedMap [TopologicalSpace Î±] [TopologicalSpace Î²] {f : Î± â†’ Î²} (hf : Inducing f)
    (h : IsClosed (range f)) : IsClosedMap f := by
  intro s hs
  -- âŠ¢ IsClosed (f '' s)
  rcases hf.isClosed_iff.1 hs with âŸ¨t, ht, rflâŸ©
  -- âŠ¢ IsClosed (f '' (f â»Â¹' t))
  rw [image_preimage_eq_inter_range]
  -- âŠ¢ IsClosed (t âˆ© range f)
  exact ht.inter h
  -- ğŸ‰ no goals
#align inducing.is_closed_map Inducing.isClosedMap

theorem isClosedMap_iff_closure_image [TopologicalSpace Î±] [TopologicalSpace Î²] {f : Î± â†’ Î²} :
    IsClosedMap f â†” âˆ€ s, closure (f '' s) âŠ† f '' closure s :=
  âŸ¨IsClosedMap.closure_image_subset, fun hs c hc =>
    isClosed_of_closure_subset <|
      calc
        closure (f '' c) âŠ† f '' closure c := hs c
        _ = f '' c := by rw [hc.closure_eq]âŸ©
                         -- ğŸ‰ no goals
#align is_closed_map_iff_closure_image isClosedMap_iff_closure_image

/-- A map `f : X â†’ Y` is closed if and only if for all sets `s`, any cluster point of `f '' s` is
the image by `f` of some cluster point of `s`.
If you require this for all filters instead of just principal filters, and also that `f` is
continuous, you get the notion of **proper map**. See `isProperMap_iff_clusterPt`. -/
theorem isClosedMap_iff_clusterPt [TopologicalSpace Î±] [TopologicalSpace Î²] {f : Î± â†’ Î²} :
    IsClosedMap f â†” âˆ€ s y, MapClusterPt y (ğ“Ÿ s) f â†’ âˆƒ x, f x = y âˆ§ ClusterPt x (ğ“Ÿ s) := by
  simp [MapClusterPt, isClosedMap_iff_closure_image, subset_def, mem_closure_iff_clusterPt,
    and_comm]

theorem IsClosedMap.closure_image_eq_of_continuous [TopologicalSpace Î±] [TopologicalSpace Î²]
    {f : Î± â†’ Î²} (f_closed : IsClosedMap f) (f_cont : Continuous f) (s : Set Î±) :
    closure (f '' s) = f '' closure s :=
  subset_antisymm (f_closed.closure_image_subset s) (image_closure_subset_closure_image f_cont)

theorem IsClosedMap.lift'_closure_map_eq [TopologicalSpace Î±] [TopologicalSpace Î²]
    {f : Î± â†’ Î²} (f_closed : IsClosedMap f) (f_cont : Continuous f) (F : Filter Î±) :
    (map f F).lift' closure = map f (F.lift' closure) := by
  rw [map_lift'_eq2 (monotone_closure Î²), map_lift'_eq (monotone_closure Î±)]
  -- âŠ¢ Filter.lift' F (closure âˆ˜ image f) = Filter.lift' F (image f âˆ˜ closure)
  congr
  -- âŠ¢ closure âˆ˜ image f = image f âˆ˜ closure
  ext s : 1
  -- âŠ¢ (closure âˆ˜ image f) s = (image f âˆ˜ closure) s
  exact f_closed.closure_image_eq_of_continuous f_cont s
  -- ğŸ‰ no goals

theorem IsClosedMap.mapClusterPt_iff_lift'_closure [TopologicalSpace Î±] [TopologicalSpace Î²]
    {F : Filter Î±} {f : Î± â†’ Î²} (f_closed : IsClosedMap f) (f_cont : Continuous f) {y : Î²} :
    MapClusterPt y F f â†” ((F.lift' closure) âŠ“ ğ“Ÿ (f â»Â¹' {y})).NeBot := by
  rw [MapClusterPt, clusterPt_iff_lift'_closure', f_closed.lift'_closure_map_eq f_cont,
      â† comap_principal, â† map_neBot_iff f, Filter.push_pull, principal_singleton]

section OpenEmbedding

variable [TopologicalSpace Î±] [TopologicalSpace Î²] [TopologicalSpace Î³]

/-- An open embedding is an embedding with open image. -/
@[mk_iff openEmbedding_iff]
structure OpenEmbedding (f : Î± â†’ Î²) extends Embedding f : Prop where
  /-- The range of an open embedding is an open set. -/
  open_range : IsOpen <| range f
#align open_embedding OpenEmbedding
#align open_embedding_iff openEmbedding_iff

theorem OpenEmbedding.isOpenMap {f : Î± â†’ Î²} (hf : OpenEmbedding f) : IsOpenMap f :=
  hf.toEmbedding.toInducing.isOpenMap hf.open_range
#align open_embedding.is_open_map OpenEmbedding.isOpenMap

theorem OpenEmbedding.map_nhds_eq {f : Î± â†’ Î²} (hf : OpenEmbedding f) (a : Î±) :
    map f (ğ“ a) = ğ“ (f a) :=
  hf.toEmbedding.map_nhds_of_mem _ <| hf.open_range.mem_nhds <| mem_range_self _
#align open_embedding.map_nhds_eq OpenEmbedding.map_nhds_eq

theorem OpenEmbedding.open_iff_image_open {f : Î± â†’ Î²} (hf : OpenEmbedding f) {s : Set Î±} :
    IsOpen s â†” IsOpen (f '' s) :=
  âŸ¨hf.isOpenMap s, fun h => by
    convert â† h.preimage hf.toEmbedding.continuous
    -- âŠ¢ f â»Â¹' (f '' s) = s
    apply preimage_image_eq _ hf.injâŸ©
    -- ğŸ‰ no goals
#align open_embedding.open_iff_image_open OpenEmbedding.open_iff_image_open

theorem OpenEmbedding.tendsto_nhds_iff {Î¹ : Type*} {f : Î¹ â†’ Î²} {g : Î² â†’ Î³} {a : Filter Î¹} {b : Î²}
    (hg : OpenEmbedding g) : Tendsto f a (ğ“ b) â†” Tendsto (g âˆ˜ f) a (ğ“ (g b)) :=
  hg.toEmbedding.tendsto_nhds_iff
#align open_embedding.tendsto_nhds_iff OpenEmbedding.tendsto_nhds_iff

theorem OpenEmbedding.tendsto_nhds_iff' {f : Î± â†’ Î²} (hf : OpenEmbedding f) {g : Î² â†’ Î³}
    {l : Filter Î³} {a : Î±} : Tendsto (g âˆ˜ f) (ğ“ a) l â†” Tendsto g (ğ“ (f a)) l := by
  rw [Tendsto, â† map_map, hf.map_nhds_eq]; rfl
  -- âŠ¢ map g (ğ“ (f a)) â‰¤ l â†” Tendsto g (ğ“ (f a)) l
                                           -- ğŸ‰ no goals

theorem OpenEmbedding.continuous {f : Î± â†’ Î²} (hf : OpenEmbedding f) : Continuous f :=
  hf.toEmbedding.continuous
#align open_embedding.continuous OpenEmbedding.continuous

theorem OpenEmbedding.open_iff_preimage_open {f : Î± â†’ Î²} (hf : OpenEmbedding f) {s : Set Î²}
    (hs : s âŠ† range f) : IsOpen s â†” IsOpen (f â»Â¹' s) := by
  rw [hf.open_iff_image_open, image_preimage_eq_inter_range, inter_eq_self_of_subset_left hs]
  -- ğŸ‰ no goals
#align open_embedding.open_iff_preimage_open OpenEmbedding.open_iff_preimage_open

theorem openEmbedding_of_embedding_open {f : Î± â†’ Î²} (hâ‚ : Embedding f) (hâ‚‚ : IsOpenMap f) :
    OpenEmbedding f :=
  âŸ¨hâ‚, hâ‚‚.isOpen_rangeâŸ©
#align open_embedding_of_embedding_open openEmbedding_of_embedding_open

theorem openEmbedding_iff_embedding_open {f : Î± â†’ Î²} :
    OpenEmbedding f â†” Embedding f âˆ§ IsOpenMap f :=
  âŸ¨fun h => âŸ¨h.1, h.isOpenMapâŸ©, fun h => openEmbedding_of_embedding_open h.1 h.2âŸ©
#align open_embedding_iff_embedding_open openEmbedding_iff_embedding_open

theorem openEmbedding_of_continuous_injective_open {f : Î± â†’ Î²} (hâ‚ : Continuous f)
    (hâ‚‚ : Injective f) (hâ‚ƒ : IsOpenMap f) : OpenEmbedding f := by
  simp only [openEmbedding_iff_embedding_open, embedding_iff, inducing_iff_nhds, *, and_true_iff]
  -- âŠ¢ âˆ€ (a : Î±), ğ“ a = comap f (ğ“ (f a))
  exact fun a =>
    le_antisymm (hâ‚.tendsto _).le_comap (@comap_map _ _ (ğ“ a) _ hâ‚‚ â–¸ comap_mono (hâ‚ƒ.nhds_le _))
#align open_embedding_of_continuous_injective_open openEmbedding_of_continuous_injective_open

theorem openEmbedding_iff_continuous_injective_open {f : Î± â†’ Î²} :
    OpenEmbedding f â†” Continuous f âˆ§ Injective f âˆ§ IsOpenMap f :=
  âŸ¨fun h => âŸ¨h.continuous, h.inj, h.isOpenMapâŸ©, fun h =>
    openEmbedding_of_continuous_injective_open h.1 h.2.1 h.2.2âŸ©
#align open_embedding_iff_continuous_injective_open openEmbedding_iff_continuous_injective_open

theorem openEmbedding_id : OpenEmbedding (@id Î±) :=
  âŸ¨embedding_id, IsOpenMap.id.isOpen_rangeâŸ©
#align open_embedding_id openEmbedding_id

protected theorem OpenEmbedding.comp {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : OpenEmbedding g)
    (hf : OpenEmbedding f) : OpenEmbedding (g âˆ˜ f) :=
  âŸ¨hg.1.comp hf.1, (hg.isOpenMap.comp hf.isOpenMap).isOpen_rangeâŸ©
#align open_embedding.comp OpenEmbedding.comp

theorem OpenEmbedding.isOpenMap_iff {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : OpenEmbedding g) :
    IsOpenMap f â†” IsOpenMap (g âˆ˜ f) := by
  simp_rw [isOpenMap_iff_nhds_le, â† map_map, comp, â† hg.map_nhds_eq, Filter.map_le_map_iff hg.inj]
  -- ğŸ‰ no goals
#align open_embedding.is_open_map_iff OpenEmbedding.isOpenMap_iff

theorem OpenEmbedding.of_comp_iff (f : Î± â†’ Î²) {g : Î² â†’ Î³} (hg : OpenEmbedding g) :
    OpenEmbedding (g âˆ˜ f) â†” OpenEmbedding f := by
  simp only [openEmbedding_iff_continuous_injective_open, â† hg.isOpenMap_iff, â†
    hg.1.continuous_iff, hg.inj.of_comp_iff]
#align open_embedding.of_comp_iff OpenEmbedding.of_comp_iff

theorem OpenEmbedding.of_comp (f : Î± â†’ Î²) {g : Î² â†’ Î³} (hg : OpenEmbedding g)
    (h : OpenEmbedding (g âˆ˜ f)) : OpenEmbedding f :=
  (OpenEmbedding.of_comp_iff f hg).1 h
#align open_embedding.of_comp OpenEmbedding.of_comp

end OpenEmbedding

section ClosedEmbedding

variable [TopologicalSpace Î±] [TopologicalSpace Î²] [TopologicalSpace Î³]

/-- A closed embedding is an embedding with closed image. -/
@[mk_iff closedEmbedding_iff]
structure ClosedEmbedding (f : Î± â†’ Î²) extends Embedding f : Prop where
  /-- The range of a closed embedding is a closed set. -/
  closed_range : IsClosed <| range f
#align closed_embedding ClosedEmbedding
#align closed_embedding_iff closedEmbedding_iff

variable {f : Î± â†’ Î²}

theorem ClosedEmbedding.tendsto_nhds_iff {Î¹ : Type*} {g : Î¹ â†’ Î±} {a : Filter Î¹} {b : Î±}
    (hf : ClosedEmbedding f) : Tendsto g a (ğ“ b) â†” Tendsto (f âˆ˜ g) a (ğ“ (f b)) :=
  hf.toEmbedding.tendsto_nhds_iff
#align closed_embedding.tendsto_nhds_iff ClosedEmbedding.tendsto_nhds_iff

theorem ClosedEmbedding.continuous (hf : ClosedEmbedding f) : Continuous f :=
  hf.toEmbedding.continuous
#align closed_embedding.continuous ClosedEmbedding.continuous

theorem ClosedEmbedding.isClosedMap (hf : ClosedEmbedding f) : IsClosedMap f :=
  hf.toEmbedding.toInducing.isClosedMap hf.closed_range
#align closed_embedding.is_closed_map ClosedEmbedding.isClosedMap

theorem ClosedEmbedding.closed_iff_image_closed (hf : ClosedEmbedding f) {s : Set Î±} :
    IsClosed s â†” IsClosed (f '' s) :=
  âŸ¨hf.isClosedMap s, fun h => by
    rw [â† preimage_image_eq s hf.inj]
    -- âŠ¢ IsClosed (f â»Â¹' (f '' s))
    exact h.preimage hf.continuousâŸ©
    -- ğŸ‰ no goals
#align closed_embedding.closed_iff_image_closed ClosedEmbedding.closed_iff_image_closed

theorem ClosedEmbedding.closed_iff_preimage_closed (hf : ClosedEmbedding f) {s : Set Î²}
    (hs : s âŠ† range f) : IsClosed s â†” IsClosed (f â»Â¹' s) := by
  rw [hf.closed_iff_image_closed, image_preimage_eq_of_subset hs]
  -- ğŸ‰ no goals
#align closed_embedding.closed_iff_preimage_closed ClosedEmbedding.closed_iff_preimage_closed

theorem closedEmbedding_of_embedding_closed (hâ‚ : Embedding f) (hâ‚‚ : IsClosedMap f) :
    ClosedEmbedding f :=
  âŸ¨hâ‚, image_univ (f := f) â–¸ hâ‚‚ univ isClosed_univâŸ©
#align closed_embedding_of_embedding_closed closedEmbedding_of_embedding_closed

theorem closedEmbedding_of_continuous_injective_closed (hâ‚ : Continuous f) (hâ‚‚ : Injective f)
    (hâ‚ƒ : IsClosedMap f) : ClosedEmbedding f := by
  refine closedEmbedding_of_embedding_closed âŸ¨âŸ¨?_âŸ©, hâ‚‚âŸ© hâ‚ƒ
  -- âŠ¢ instâœÂ² = induced f instâœÂ¹
  refine hâ‚.le_induced.antisymm fun s hs => ?_
  -- âŠ¢ IsOpen s
  refine âŸ¨(f '' sá¶œ)á¶œ, (hâ‚ƒ _ hs.isClosed_compl).isOpen_compl, ?_âŸ©
  -- âŠ¢ f â»Â¹' (f '' sá¶œ)á¶œ = s
  rw [preimage_compl, preimage_image_eq _ hâ‚‚, compl_compl]
  -- ğŸ‰ no goals
#align closed_embedding_of_continuous_injective_closed closedEmbedding_of_continuous_injective_closed

theorem closedEmbedding_id : ClosedEmbedding (@id Î±) :=
  âŸ¨embedding_id, IsClosedMap.id.closed_rangeâŸ©
#align closed_embedding_id closedEmbedding_id

theorem ClosedEmbedding.comp {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : ClosedEmbedding g)
    (hf : ClosedEmbedding f) : ClosedEmbedding (g âˆ˜ f) :=
  âŸ¨hg.toEmbedding.comp hf.toEmbedding, (hg.isClosedMap.comp hf.isClosedMap).closed_rangeâŸ©
#align closed_embedding.comp ClosedEmbedding.comp

theorem ClosedEmbedding.closure_image_eq {f : Î± â†’ Î²} (hf : ClosedEmbedding f) (s : Set Î±) :
    closure (f '' s) = f '' closure s :=
  hf.isClosedMap.closure_image_eq_of_continuous hf.continuous s
#align closed_embedding.closure_image_eq ClosedEmbedding.closure_image_eq

end ClosedEmbedding
