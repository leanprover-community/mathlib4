/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
module

public import Mathlib.Order.Filter.CountableInter
public import Mathlib.Topology.Defs.Induced
public import Mathlib.Data.Set.Notation
import Mathlib.Topology.Constructions

/-!
# `GÎ´` sets

In this file we define `GÎ´` sets and prove their basic properties.

## Main definitions

* `IsGÎ´`: a set `s` is a `GÎ´` set if it can be represented as an intersection
  of countably many open sets;

* `residual`: the Ïƒ-filter of residual sets. A set `s` is called *residual* if it includes a
  countable intersection of dense open sets.

* `IsNowhereDense`: a set is called *nowhere dense* iff its closure has empty interior
* `IsMeagre`: a set `s` is called *meagre* iff its complement is residual

## Main results

We prove that finite or countable intersections of GÎ´ sets are GÎ´ sets.

- `isClosed_isNowhereDense_iff_compl`: a closed set is nowhere dense iff
its complement is open and dense
- `isMeagre_iff_countable_union_isNowhereDense`: a set is meagre iff it is contained in a countable
union of nowhere dense sets
- subsets of meagre sets are meagre; countable unions of meagre sets are meagre

See `Mathlib/Topology/GDelta/MetrizableSpace.lean` for the proof that
continuity set of a function from a topological space to a metrizable space is a GÎ´ set.

## Tags

GÎ´ set, residual set, nowhere dense set, meagre set
-/

@[expose] public section

assert_not_exists UniformSpace

noncomputable section

open Topology TopologicalSpace Filter Encodable Set

variable {X Y Î¹ : Type*} {Î¹' : Sort*}


section IsGÎ´

variable [TopologicalSpace X]

/-- A GÎ´ set is a countable intersection of open sets. -/
def IsGÎ´ (s : Set X) : Prop :=
  âˆƒ T : Set (Set X), (âˆ€ t âˆˆ T, IsOpen t) âˆ§ T.Countable âˆ§ s = â‹‚â‚€ T

/-- An open set is a GÎ´ set. -/
theorem IsOpen.isGÎ´ {s : Set X} (h : IsOpen s) : IsGÎ´ s :=
  âŸ¨{s}, by simp [h], countable_singleton _, (Set.sInter_singleton _).symmâŸ©

@[simp]
protected theorem IsGÎ´.empty : IsGÎ´ (âˆ… : Set X) :=
  isOpen_empty.isGÎ´


@[simp]
protected theorem IsGÎ´.univ : IsGÎ´ (univ : Set X) :=
  isOpen_univ.isGÎ´


theorem IsGÎ´.biInter_of_isOpen {I : Set Î¹} (hI : I.Countable) {f : Î¹ â†’ Set X}
    (hf : âˆ€ i âˆˆ I, IsOpen (f i)) : IsGÎ´ (â‹‚ i âˆˆ I, f i) :=
  âŸ¨f '' I, by rwa [forall_mem_image], hI.image _, by rw [sInter_image]âŸ©


theorem IsGÎ´.iInter_of_isOpen [Countable Î¹'] {f : Î¹' â†’ Set X} (hf : âˆ€ i, IsOpen (f i)) :
    IsGÎ´ (â‹‚ i, f i) :=
  âŸ¨range f, by rwa [forall_mem_range], countable_range _, by rw [sInter_range]âŸ©


lemma isGÎ´_iff_eq_iInter_nat {s : Set X} :
    IsGÎ´ s â†” âˆƒ (f : â„• â†’ Set X), (âˆ€ n, IsOpen (f n)) âˆ§ s = â‹‚ n, f n := by
  refine âŸ¨?_, ?_âŸ©
  Â· rintro âŸ¨T, hT, T_count, rflâŸ©
    rcases Set.eq_empty_or_nonempty T with rfl | hT
    Â· exact âŸ¨fun _n â†¦ univ, fun _n â†¦ isOpen_univ, by simpâŸ©
    Â· obtain âŸ¨f, hfâŸ© : âˆƒ (f : â„• â†’ Set X), T = range f := Countable.exists_eq_range T_count hT
      exact âŸ¨f, by simp_all, by simp [hf]âŸ©
  Â· rintro âŸ¨f, hf, rflâŸ©
    exact .iInter_of_isOpen hf

alias âŸ¨IsGÎ´.eq_iInter_nat, _âŸ© := isGÎ´_iff_eq_iInter_nat

/-- The intersection of an encodable family of GÎ´ sets is a GÎ´ set. -/
protected theorem IsGÎ´.iInter [Countable Î¹'] {s : Î¹' â†’ Set X} (hs : âˆ€ i, IsGÎ´ (s i)) :
    IsGÎ´ (â‹‚ i, s i) := by
  choose T hTo hTc hTs using hs
  obtain rfl : s = fun i => â‹‚â‚€ T i := funext hTs
  refine âŸ¨â‹ƒ i, T i, ?_, countable_iUnion hTc, (sInter_iUnion _).symmâŸ©
  simpa [@forall_swap Î¹'] using hTo

theorem IsGÎ´.biInter {s : Set Î¹} (hs : s.Countable) {t : âˆ€ i âˆˆ s, Set X}
    (ht : âˆ€ (i) (hi : i âˆˆ s), IsGÎ´ (t i hi)) : IsGÎ´ (â‹‚ i âˆˆ s, t i â€¹_â€º) := by
  rw [biInter_eq_iInter]
  haveI := hs.to_subtype
  exact .iInter fun x => ht x x.2


/-- A countable intersection of GÎ´ sets is a GÎ´ set. -/
theorem IsGÎ´.sInter {S : Set (Set X)} (h : âˆ€ s âˆˆ S, IsGÎ´ s) (hS : S.Countable) : IsGÎ´ (â‹‚â‚€ S) := by
  simpa only [sInter_eq_biInter] using IsGÎ´.biInter hS h


theorem IsGÎ´.inter {s t : Set X} (hs : IsGÎ´ s) (ht : IsGÎ´ t) : IsGÎ´ (s âˆ© t) := by
  rw [inter_eq_iInter]
  exact .iInter (Bool.forall_bool.2 âŸ¨ht, hsâŸ©)

/-- The union of two GÎ´ sets is a GÎ´ set. -/
theorem IsGÎ´.union {s t : Set X} (hs : IsGÎ´ s) (ht : IsGÎ´ t) : IsGÎ´ (s âˆª t) := by
  rcases hs with âŸ¨S, Sopen, Scount, rflâŸ©
  rcases ht with âŸ¨T, Topen, Tcount, rflâŸ©
  rw [sInter_union_sInter]
  refine .biInter_of_isOpen (Scount.prod Tcount) ?_
  rintro âŸ¨a, bâŸ© âŸ¨ha, hbâŸ©
  exact (Sopen a ha).union (Topen b hb)

/-- The union of finitely many GÎ´ sets is a GÎ´ set, `Set.sUnion` version. -/
theorem IsGÎ´.sUnion {S : Set (Set X)} (hS : S.Finite) (h : âˆ€ s âˆˆ S, IsGÎ´ s) : IsGÎ´ (â‹ƒâ‚€ S) := by
  induction S, hS using Set.Finite.induction_on with
  | empty => simp
  | insert _ _ ih =>
    simp only [forall_mem_insert, sUnion_insert] at *
    exact h.1.union (ih h.2)

/-- The union of finitely many GÎ´ sets is a GÎ´ set, bounded indexed union version. -/
theorem IsGÎ´.biUnion {s : Set Î¹} (hs : s.Finite) {f : Î¹ â†’ Set X} (h : âˆ€ i âˆˆ s, IsGÎ´ (f i)) :
    IsGÎ´ (â‹ƒ i âˆˆ s, f i) := by
  rw [â† sUnion_image]
  exact .sUnion (hs.image _) (forall_mem_image.2 h)

/-- The union of finitely many GÎ´ sets is a GÎ´ set, bounded indexed union version. -/
theorem IsGÎ´.iUnion [Finite Î¹'] {f : Î¹' â†’ Set X} (h : âˆ€ i, IsGÎ´ (f i)) : IsGÎ´ (â‹ƒ i, f i) :=
  .sUnion (finite_range _) <| forall_mem_range.2 h

/- The preimage of a GÎ´ set under a continuous map is GÎ´. -/
theorem isGÎ´_induced [TopologicalSpace Y] {f : X â†’ Y} {s : Set Y} (hf : Continuous f)
    (hs : IsGÎ´ s) : IsGÎ´ (f â»Â¹' s) := by
  obtain âŸ¨U, hU1, hU2âŸ© := hs.eq_iInter_nat
  simp_all only [preimage_iInter]
  exact IsGÎ´.iInter_of_isOpen (fun i => hf.isOpen_preimage (U i) (hU1 i))

end IsGÎ´

section residual

variable [TopologicalSpace X]

/-- A set `s` is called *residual* if it includes a countable intersection of dense open sets. -/
def residual (X : Type*) [TopologicalSpace X] : Filter X :=
  Filter.countableGenerate { t | IsOpen t âˆ§ Dense t }

instance countableInterFilter_residual : CountableInterFilter (residual X) := by
  rw [residual]; infer_instance

/-- Dense open sets are residual. -/
theorem residual_of_dense_open {s : Set X} (ho : IsOpen s) (hd : Dense s) : s âˆˆ residual X :=
  CountableGenerateSets.basic âŸ¨ho, hdâŸ©

/-- Dense GÎ´ sets are residual. -/
theorem residual_of_dense_GÎ´ {s : Set X} (ho : IsGÎ´ s) (hd : Dense s) : s âˆˆ residual X := by
  rcases ho with âŸ¨T, To, Tct, rflâŸ©
  exact
    (countable_sInter_mem Tct).mpr fun t tT =>
      residual_of_dense_open (To t tT) (hd.mono (sInter_subset_of_mem tT))

/-- A set is residual iff it includes a countable intersection of dense open sets. -/
theorem mem_residual_iff {s : Set X} :
    s âˆˆ residual X â†”
      âˆƒ S : Set (Set X), (âˆ€ t âˆˆ S, IsOpen t) âˆ§ (âˆ€ t âˆˆ S, Dense t) âˆ§ S.Countable âˆ§ â‹‚â‚€ S âŠ† s :=
  mem_countableGenerate_iff.trans <| by simp_rw [subset_def, mem_setOf, forall_and, and_assoc]

end residual

section IsMeagre
open Function TopologicalSpace Set
variable [TopologicalSpace X]

/-- A set is called **nowhere dense** iff its closure has empty interior. -/
def IsNowhereDense (s : Set X) := interior (closure s) = âˆ…

/-- The empty set is nowhere dense. -/
@[simp]
lemma isNowhereDense_empty : IsNowhereDense (âˆ… : Set X) := by
  rw [IsNowhereDense, closure_empty, interior_empty]

/-- A subset of a nowhere dense set is nowhere dense. -/
@[gcongr]
lemma IsNowhereDense.mono {s t : Set X} (ht : t âŠ† s) (hs : IsNowhereDense s) : IsNowhereDense t :=
  Set.eq_empty_of_subset_empty <| by grw [ht]; rw [hs]

/-- A closed set is nowhere dense iff its interior is empty. -/
lemma IsClosed.isNowhereDense_iff {s : Set X} (hs : IsClosed s) :
    IsNowhereDense s â†” interior s = âˆ… := by
  rw [IsNowhereDense, IsClosed.closure_eq hs]

/-- If a set `s` is nowhere dense, so is its closure. -/
protected lemma IsNowhereDense.closure {s : Set X} (hs : IsNowhereDense s) :
    IsNowhereDense (closure s) := by
  rwa [IsNowhereDense, closure_closure]

/-- A nowhere dense set `s` is contained in a closed nowhere dense set (namely, its closure). -/
lemma IsNowhereDense.subset_of_closed_isNowhereDense {s : Set X} (hs : IsNowhereDense s) :
    âˆƒ t : Set X, s âŠ† t âˆ§ IsNowhereDense t âˆ§ IsClosed t :=
  âŸ¨closure s, subset_closure, âŸ¨hs.closure, isClosed_closureâŸ©âŸ©

/-- A set `s` is closed and nowhere dense iff its complement `sá¶œ` is open and dense. -/
lemma isClosed_isNowhereDense_iff_compl {s : Set X} :
    IsClosed s âˆ§ IsNowhereDense s â†” IsOpen sá¶œ âˆ§ Dense sá¶œ := by
  rw [and_congr_right IsClosed.isNowhereDense_iff,
    isOpen_compl_iff, interior_eq_empty_iff_dense_compl]

/-- To check that `s` is nowhere dense, it suffices to check that no point of `s`
is in the interior of `closure s`. -/
lemma isNowhereDense_iff_disjoint {s : Set X} :
    IsNowhereDense s â†” Disjoint s (interior (closure s)) :=
  âŸ¨fun H â†¦ H â–¸ disjoint_empty _, fun H â†¦
    H.closure_left isOpen_interior |>.mono_left interior_subset |>.eq_bot_of_selfâŸ©

/-- To check that `s` is nowhere dense, it suffices to check that `closure s` is not a
neighborhood of any point of `s`. -/
lemma isNowhereDense_iff_forall_notMem_nhds {s : Set X} :
    IsNowhereDense s â†” âˆ€ x âˆˆ s, closure s âˆ‰ ğ“ x := by
  simp [isNowhereDense_iff_disjoint, disjoint_iff_inter_eq_empty, eq_empty_iff_forall_notMem,
    mem_interior_iff_mem_nhds]

/-- The image of a nowhere dense set through an inducing map is nowhere dense. -/
lemma Topology.IsInducing.isNowhereDense_image [TopologicalSpace Y] {f : X â†’ Y}
    (hf : Topology.IsInducing f) {s : Set X} (h : IsNowhereDense s) : IsNowhereDense (f '' s) := by
  rw [isNowhereDense_iff_forall_notMem_nhds, forall_mem_image] at *
  simp_rw [hf.nhds_eq_comap, hf.closure_eq_preimage_closure_image] at h
  exact fun x x_mem hx â†¦ h x x_mem (preimage_mem_comap hx)

/-- A set is nowhere dense if it is nowhere dense in some subspace. -/
lemma IsNowhereDense.image_val {Y : Set X} {s : Set Y}
    (hs : IsNowhereDense s) : IsNowhereDense (s : Set X) :=
  Topology.IsInducing.subtypeVal.isNowhereDense_image hs

/-- A set is called **meagre** iff its complement is a residual (or comeagre) set. -/
def IsMeagre (s : Set X) := sá¶œ âˆˆ residual X

/-- The empty set is meagre. -/
lemma IsMeagre.empty : IsMeagre (âˆ… : Set X) := by
  rw [IsMeagre, compl_empty]
  exact Filter.univ_mem

/-- Subsets of meagre sets are meagre. -/
@[gcongr]
lemma IsMeagre.mono {s t : Set X} (hts : t âŠ† s) (hs : IsMeagre s) : IsMeagre t :=
  Filter.mem_of_superset hs (compl_subset_compl.mpr hts)

/-- An intersection with a meagre set is meagre. -/
lemma IsMeagre.inter {s t : Set X} (hs : IsMeagre s) : IsMeagre (s âˆ© t) :=
  hs.mono inter_subset_left

/-- A union of two meagre sets is meagre. -/
lemma IsMeagre.union {s t : Set X} (hs : IsMeagre s) (ht : IsMeagre t) : IsMeagre (s âˆª t) := by
  rw [IsMeagre, compl_union]
  exact inter_mem hs ht

/-- A countable union of meagre sets is meagre. -/
lemma isMeagre_iUnion [Countable Î¹'] {f : Î¹' â†’ Set X} (hs : âˆ€ i, IsMeagre (f i)) :
    IsMeagre (â‹ƒ i, f i) := by
  rw [IsMeagre, compl_iUnion]
  exact countable_iInter_mem.mpr hs

lemma isMeagre_biUnion {I : Set Î¹} (c : I.Countable) {f : Î¹ â†’ Set X}
    (h : âˆ€ i âˆˆ I, IsMeagre (f i)) : IsMeagre (â‹ƒ i âˆˆ I, f i) := by
  suffices IsMeagre (â‹ƒ i : I, f i) by simpa
  have : Countable I := c
  apply isMeagre_iUnion
  intro âŸ¨i, hiâŸ©
  exact h i hi

/-- A set is meagre iff it is contained in a countable union of nowhere dense sets. -/
lemma isMeagre_iff_countable_union_isNowhereDense {s : Set X} :
    IsMeagre s â†” âˆƒ S : Set (Set X), (âˆ€ t âˆˆ S, IsNowhereDense t) âˆ§ S.Countable âˆ§ s âŠ† â‹ƒâ‚€ S := by
  rw [IsMeagre, mem_residual_iff, compl_bijective.surjective.image_surjective.exists]
  simp_rw [â† and_assoc, â† forall_and, forall_mem_image, â† isClosed_isNowhereDense_iff_compl,
    sInter_image, â† compl_iUnionâ‚‚, compl_subset_compl, â† sUnion_eq_biUnion, and_assoc]
  refine âŸ¨fun âŸ¨S, hS, hc, hsubâŸ© â†¦ âŸ¨S, fun s hs â†¦ (hS hs).2, ?_, hsubâŸ©, ?_âŸ©
  Â· rw [â† compl_compl_image S]; exact hc.image _
  Â· intro âŸ¨S, hS, hc, hsubâŸ©
    use closure '' S
    rw [forall_mem_image]
    exact âŸ¨fun s hs â†¦ âŸ¨isClosed_closure, (hS s hs).closureâŸ©,
      (hc.image _).image _, hsub.trans (sUnion_mono_subsets fun s â†¦ subset_closure)âŸ©

/-- A set of second category (i.e. non-meagre) is nonempty. -/
lemma nonempty_of_not_isMeagre {s : Set X} (hs : Â¬IsMeagre s) : s.Nonempty := by
  contrapose! hs
  simpa [hs] using IsMeagre.empty

/-- A nowhere dense set is meagre. -/
lemma IsNowhereDense.isMeagre {s : Set X} (h : IsNowhereDense s) : IsMeagre s := by
  rw [isMeagre_iff_countable_union_isNowhereDense]
  exact âŸ¨{s}, by simpa, by simp, by simpâŸ©

lemma exists_of_not_isMeagre_biUnion {I : Set Î¹}
    (c : I.Countable) {A : Î¹ â†’ Set X} (h : Â¬IsMeagre (â‹ƒ i âˆˆ I, A i)) :
    âˆƒ i âˆˆ I, Â¬IsMeagre (A i) := by
  contrapose! h
  exact isMeagre_biUnion c h

/-- The image of a meagre set through an inducing map is meagre. -/
lemma Topology.IsInducing.isMeagre_image [TopologicalSpace Y] {f : X â†’ Y}
    (hf : Topology.IsInducing f) {s : Set X} (h : IsMeagre s) : IsMeagre (f '' s) := by
  rw [isMeagre_iff_countable_union_isNowhereDense] at *
  obtain âŸ¨T, isNowhereDense, countable, coverâŸ© := h
  refine âŸ¨(Set.image f) '' T, ?isNowhereDense, countable.image _, ?coverâŸ©
  case isNowhereDense =>
    intro u âŸ¨t, tT, tuâŸ©
    rw [â† tu]
    apply hf.isNowhereDense_image (isNowhereDense t tT)
  case cover =>
    rw [â† Set.image_sUnion]
    grw [cover]

/-- A set is meagre if it is meagre in some subspace. -/
lemma IsMeagre.image_val {s : Set X} {m : Set s} (h : IsMeagre (m : Set s)) :
    IsMeagre (m : Set X) := Topology.IsInducing.subtypeVal.isMeagre_image h

end IsMeagre
