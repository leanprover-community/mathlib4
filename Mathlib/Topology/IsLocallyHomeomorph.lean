/-
Copyright (c) 2021 Thomas Browning. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Thomas Browning
-/
import Mathlib.Topology.LocalHomeomorph
import Mathlib.Topology.SeparatedMap

#align_import topology.is_locally_homeomorph from "leanprover-community/mathlib"@"e97cf15cd1aec9bd5c193b2ffac5a6dc9118912b"

/-!
# Local homeomorphisms

This file defines local homeomorphisms.

## Main definitions

* `IsLocallyHomeomorph`: A function `f : X ‚Üí Y` satisfies `IsLocallyHomeomorph` if for each
  point `x : X`, the restriction of `f` to some open neighborhood `U` of `x` gives a homeomorphism
  between `U` and an open subset of `Y`.

  Note that `IsLocallyHomeomorph` is a global condition. This is in contrast to
  `LocalHomeomorph`, which is a homeomorphism between specific open subsets.
-/


open Topology

variable {X Y Z : Type*} [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z] (g : Y ‚Üí Z)
  (f : X ‚Üí Y) (s : Set X) (t : Set Y)

/-- A function `f : X ‚Üí Y` satisfies `IsLocallyHomeomorphOn f s` if each `x ‚àà s` is contained in
the source of some `e : LocalHomeomorph X Y` with `f = e`. -/
def IsLocallyHomeomorphOn :=
  ‚àÄ x ‚àà s, ‚àÉ e : LocalHomeomorph X Y, x ‚àà e.source ‚àß f = e
#align is_locally_homeomorph_on IsLocallyHomeomorphOn

theorem isLocallyHomeomorphOn_iff_openEmbedding_restrict {f : X ‚Üí Y} :
    IsLocallyHomeomorphOn f s ‚Üî ‚àÄ x ‚àà s, ‚àÉ U ‚àà ùìù x, OpenEmbedding (U.restrict f) := by
  refine ‚ü®fun h x hx ‚Ü¶ ?_, fun h x hx ‚Ü¶ ?_‚ü©
  ¬∑ obtain ‚ü®e, hxe, rfl‚ü© := h x hx
    exact ‚ü®e.source, e.open_source.mem_nhds hxe, e.openEmbedding_restrict‚ü©
  ¬∑ obtain ‚ü®U, hU, emb‚ü© := h x hx
    have : OpenEmbedding ((interior U).restrict f)
    ¬∑ refine emb.comp ‚ü®embedding_inclusion interior_subset, ?_‚ü©
      rw [Set.range_inclusion]; exact isOpen_induced isOpen_interior
    obtain ‚ü®cont, inj, openMap‚ü© := openEmbedding_iff_continuous_injective_open.mp this
    haveI : Nonempty X := ‚ü®x‚ü©
    exact ‚ü®LocalHomeomorph.ofContinuousOpenRestrict (Set.injOn_iff_injective.mpr inj).toLocalEquiv
      (continuousOn_iff_continuous_restrict.mpr cont) openMap isOpen_interior,
      mem_interior_iff_mem_nhds.mpr hU, rfl‚ü©

namespace IsLocallyHomeomorphOn

/-- Proves that `f` satisfies `IsLocallyHomeomorphOn f s`. The condition `h` is weaker than the
definition of `IsLocallyHomeomorphOn f s`, since it only requires `e : LocalHomeomorph X Y` to
agree with `f` on its source `e.source`, as opposed to on the whole space `X`. -/
theorem mk (h : ‚àÄ x ‚àà s, ‚àÉ e : LocalHomeomorph X Y, x ‚àà e.source ‚àß ‚àÄ y ‚àà e.source, f y = e y) :
    IsLocallyHomeomorphOn f s := by
  intro x hx
  obtain ‚ü®e, hx, he‚ü© := h x hx
  exact
    ‚ü®{ e with
        toFun := f
        map_source' := fun x hx => by rw [he x hx]; exact e.map_source' hx
        left_inv' := fun x hx => by rw [he x hx]; exact e.left_inv' hx
        right_inv' := fun y hy => by rw [he _ (e.map_target' hy)]; exact e.right_inv' hy
        continuous_toFun := (continuousOn_congr he).mpr e.continuous_toFun },
      hx, rfl‚ü©
#align is_locally_homeomorph_on.mk IsLocallyHomeomorphOn.mk

variable {g f s t}

theorem mono {t : Set X} (hf : IsLocallyHomeomorphOn f t) (hst : s ‚äÜ t) :
    IsLocallyHomeomorphOn f s := fun x hx ‚Ü¶ hf x (hst hx)

theorem of_comp_left (hgf : IsLocallyHomeomorphOn (g ‚àò f) s) (hg : IsLocallyHomeomorphOn g (f '' s))
    (cont : ‚àÄ x ‚àà s, ContinuousAt f x) : IsLocallyHomeomorphOn f s := mk f s fun x hx ‚Ü¶ by
  obtain ‚ü®g, hxg, rfl‚ü© := hg (f x) ‚ü®x, hx, rfl‚ü©
  obtain ‚ü®gf, hgf, he‚ü© := hgf x hx
  refine ‚ü®(gf.restr <| f ‚Åª¬π' g.source).trans g.symm, ‚ü®‚ü®hgf, mem_interior_iff_mem_nhds.mpr
    ((cont x hx).preimage_mem_nhds <| g.open_source.mem_nhds hxg)‚ü©, he ‚ñ∏ g.map_source hxg‚ü©,
    fun y hy ‚Ü¶ ?_‚ü©
  change f y = g.symm (gf y)
  have : f y ‚àà g.source := by apply interior_subset hy.1.2
  rw [‚Üê he, g.eq_symm_apply this (by apply g.map_source this)]
  rfl

theorem of_comp_right (hgf : IsLocallyHomeomorphOn (g ‚àò f) s) (hf : IsLocallyHomeomorphOn f s) :
    IsLocallyHomeomorphOn g (f '' s) := mk g _ <| by
  rintro _ ‚ü®x, hx, rfl‚ü©
  obtain ‚ü®f, hxf, rfl‚ü© := hf x hx
  obtain ‚ü®gf, hgf, he‚ü© := hgf x hx
  refine ‚ü®f.symm.trans gf, ‚ü®f.map_source hxf, ?_‚ü©, fun y hy ‚Ü¶ ?_‚ü©
  ¬∑ apply (f.left_inv hxf).symm ‚ñ∏ hgf
  ¬∑ change g y = gf (f.symm y)
    rw [‚Üê he, Function.comp_apply, f.right_inv hy.1]

theorem map_nhds_eq (hf : IsLocallyHomeomorphOn f s) {x : X} (hx : x ‚àà s) : (ùìù x).map f = ùìù (f x) :=
  let ‚ü®e, hx, he‚ü© := hf x hx
  he.symm ‚ñ∏ e.map_nhds_eq hx
#align is_locally_homeomorph_on.map_nhds_eq IsLocallyHomeomorphOn.map_nhds_eq

protected theorem continuousAt (hf : IsLocallyHomeomorphOn f s) {x : X} (hx : x ‚àà s) :
    ContinuousAt f x :=
  (hf.map_nhds_eq hx).le
#align is_locally_homeomorph_on.continuous_at IsLocallyHomeomorphOn.continuousAt

protected theorem continuousOn (hf : IsLocallyHomeomorphOn f s) : ContinuousOn f s :=
  ContinuousAt.continuousOn fun _x => hf.continuousAt
#align is_locally_homeomorph_on.continuous_on IsLocallyHomeomorphOn.continuousOn

protected theorem comp (hg : IsLocallyHomeomorphOn g t) (hf : IsLocallyHomeomorphOn f s)
    (h : Set.MapsTo f s t) : IsLocallyHomeomorphOn (g ‚àò f) s := by
  intro x hx
  obtain ‚ü®eg, hxg, rfl‚ü© := hg (f x) (h hx)
  obtain ‚ü®ef, hxf, rfl‚ü© := hf x hx
  exact ‚ü®ef.trans eg, ‚ü®hxf, hxg‚ü©, rfl‚ü©
#align is_locally_homeomorph_on.comp IsLocallyHomeomorphOn.comp

end IsLocallyHomeomorphOn

/-- A function `f : X ‚Üí Y` satisfies `IsLocallyHomeomorph f` if each `x : x` is contained in
  the source of some `e : LocalHomeomorph X Y` with `f = e`. -/
def IsLocallyHomeomorph :=
  ‚àÄ x : X, ‚àÉ e : LocalHomeomorph X Y, x ‚àà e.source ‚àß f = e
#align is_locally_homeomorph IsLocallyHomeomorph

theorem Homeomorph.isLocallyHomeomorph (f : X ‚âÉ‚Çú Y) : IsLocallyHomeomorph f :=
  fun _ ‚Ü¶ ‚ü®f.toLocalHomeomorph, trivial, rfl‚ü©

variable {f s}

theorem isLocallyHomeomorph_iff_isLocallyHomeomorphOn_univ :
    IsLocallyHomeomorph f ‚Üî IsLocallyHomeomorphOn f Set.univ :=
  ‚ü®fun h x _ ‚Ü¶ h x, fun h x ‚Ü¶ h x trivial‚ü©
#align is_locally_homeomorph_iff_is_locally_homeomorph_on_univ isLocallyHomeomorph_iff_isLocallyHomeomorphOn_univ

protected theorem IsLocallyHomeomorph.isLocallyHomeomorphOn (hf : IsLocallyHomeomorph f) :
    IsLocallyHomeomorphOn f s := fun x _ ‚Ü¶ hf x
#align is_locally_homeomorph.is_locally_homeomorph_on IsLocallyHomeomorph.isLocallyHomeomorphOn

theorem isLocallyHomeomorph_iff_openEmbedding_restrict {f : X ‚Üí Y} :
    IsLocallyHomeomorph f ‚Üî ‚àÄ x : X, ‚àÉ U ‚àà ùìù x, OpenEmbedding (U.restrict f) := by
  simp_rw [isLocallyHomeomorph_iff_isLocallyHomeomorphOn_univ,
    isLocallyHomeomorphOn_iff_openEmbedding_restrict, imp_iff_right (Set.mem_univ _)]

theorem OpenEmbedding.isLocallyHomeomorph (hf : OpenEmbedding f) : IsLocallyHomeomorph f :=
  isLocallyHomeomorph_iff_openEmbedding_restrict.mpr fun _ ‚Ü¶
    ‚ü®_, Filter.univ_mem, hf.comp (Homeomorph.Set.univ X).openEmbedding‚ü©

variable (f)

namespace IsLocallyHomeomorph

/-- Proves that `f` satisfies `IsLocallyHomeomorph f`. The condition `h` is weaker than the
definition of `IsLocallyHomeomorph f`, since it only requires `e : LocalHomeomorph X Y` to
agree with `f` on its source `e.source`, as opposed to on the whole space `X`. -/
theorem mk (h : ‚àÄ x : X, ‚àÉ e : LocalHomeomorph X Y, x ‚àà e.source ‚àß ‚àÄ y ‚àà e.source, f y = e y) :
    IsLocallyHomeomorph f :=
  isLocallyHomeomorph_iff_isLocallyHomeomorphOn_univ.mpr
    (IsLocallyHomeomorphOn.mk f Set.univ fun x _hx => h x)
#align is_locally_homeomorph.mk IsLocallyHomeomorph.mk

variable {g f}

lemma isLocallyInjective (hf : IsLocallyHomeomorph f) : IsLocallyInjective f :=
  fun x ‚Ü¶ by obtain ‚ü®f, hx, rfl‚ü© := hf x; exact ‚ü®f.source, f.open_source, hx, f.injOn‚ü©

theorem of_comp (hgf : IsLocallyHomeomorph (g ‚àò f)) (hg : IsLocallyHomeomorph g)
    (cont : Continuous f) : IsLocallyHomeomorph f :=
  isLocallyHomeomorph_iff_isLocallyHomeomorphOn_univ.mpr <|
    hgf.isLocallyHomeomorphOn.of_comp_left hg.isLocallyHomeomorphOn fun _ _ ‚Ü¶ cont.continuousAt

theorem map_nhds_eq (hf : IsLocallyHomeomorph f) (x : X) : (ùìù x).map f = ùìù (f x) :=
  hf.isLocallyHomeomorphOn.map_nhds_eq (Set.mem_univ x)
#align is_locally_homeomorph.map_nhds_eq IsLocallyHomeomorph.map_nhds_eq

protected theorem continuous (hf : IsLocallyHomeomorph f) : Continuous f :=
  continuous_iff_continuousOn_univ.mpr hf.isLocallyHomeomorphOn.continuousOn
#align is_locally_homeomorph.continuous IsLocallyHomeomorph.continuous

protected theorem isOpenMap (hf : IsLocallyHomeomorph f) : IsOpenMap f :=
  IsOpenMap.of_nhds_le fun x => ge_of_eq (hf.map_nhds_eq x)
#align is_locally_homeomorph.is_open_map IsLocallyHomeomorph.isOpenMap

protected theorem comp (hg : IsLocallyHomeomorph g) (hf : IsLocallyHomeomorph f) :
    IsLocallyHomeomorph (g ‚àò f) :=
  isLocallyHomeomorph_iff_isLocallyHomeomorphOn_univ.mpr
    (hg.isLocallyHomeomorphOn.comp hf.isLocallyHomeomorphOn (Set.univ.mapsTo_univ f))
#align is_locally_homeomorph.comp IsLocallyHomeomorph.comp

theorem openEmbedding_of_injective (hf : IsLocallyHomeomorph f) (hi : f.Injective) :
    OpenEmbedding f :=
  openEmbedding_of_continuous_injective_open hf.continuous hi hf.isOpenMap

/-- Continuous local sections of a local homeomorphism are open embeddings. -/
theorem openEmbedding_of_comp (hf : IsLocallyHomeomorph g) (hgf : OpenEmbedding (g ‚àò f))
    (cont : Continuous f) : OpenEmbedding f :=
  (hgf.isLocallyHomeomorph.of_comp hf cont).openEmbedding_of_injective hgf.inj.of_comp

open TopologicalSpace in
/-- Ranges of continuous local sections of a local homeomorphism form a basis of the source space.-/
theorem isTopologicalBasis (hf : IsLocallyHomeomorph f) : IsTopologicalBasis
    {U : Set X | ‚àÉ V : Set Y, IsOpen V ‚àß ‚àÉ s : C(V,X), f ‚àò s = (‚Üë) ‚àß Set.range s = U} := by
  refine isTopologicalBasis_of_isOpen_of_nhds ?_ fun x U hx hU ‚Ü¶ ?_
  ¬∑ rintro _ ‚ü®U, hU, s, hs, rfl‚ü©
    refine (openEmbedding_of_comp hf (hs ‚ñ∏ ‚ü®embedding_subtype_val, ?_‚ü©) s.continuous).open_range
    rwa [Subtype.range_val]
  ¬∑ obtain ‚ü®f, hxf, rfl‚ü© := hf x
    refine ‚ü®f.source ‚à© U, ‚ü®f.target ‚à© f.symm ‚Åª¬π' U, f.symm.isOpen_inter_preimage hU,
      ‚ü®_, continuousOn_iff_continuous_restrict.mp (f.continuous_invFun.mono fun _ h ‚Ü¶ h.1)‚ü©,
      ?_, (Set.range_restrict _ _).trans ?_‚ü©, ‚ü®hxf, hx‚ü©, fun _ h ‚Ü¶ h.2‚ü©
    ¬∑ ext y; exact f.right_inv y.2.1
    ¬∑ apply (f.symm_image_target_inter_eq _).trans
      rw [Set.preimage_inter, ‚Üê Set.inter_assoc, Set.inter_eq_self_of_subset_left
        f.source_preimage_target, f.source_inter_preimage_inv_preimage]

end IsLocallyHomeomorph
