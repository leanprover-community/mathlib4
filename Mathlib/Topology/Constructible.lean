/-
Copyright (c) 2024 Ya√´l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies
-/
import Mathlib.Order.BooleanSubalgebra
import Mathlib.Topology.Spectral.Hom

/-!
# Constructible sets

This file defines constructible sets, which are morally sets in a topological space which we can
make out of finite unions and intersections of open and closed sets.

Precisely, constructible sets are the boolean subalgebra generated by open retrocompact sets,
where a set is retrocompact if its intersection with every compact open set is compact.

Constructible sets are useful because the image of a constructible set under a finitely presented
morphism of schemes is a constructible set (and this is *not* true at the level of varieties).

## Main declarations

* `IsRetrocompact`: Predicate for a set to be retrocompact, namely to have its intersection with
  every compact open is compact.
* `IsConstructible`: Predicate for a set to be constructible, namely to belong to the boolean
  subalgebra generated by open retrocompact sets.
* `IsLocallyConstructible`: Predicate for a set to be locally constructible, namely to be
  partitionable along an open cover such that each of its parts is constructible.
-/

open Set TopologicalSpace Topology

variable {Œπ : Sort*} {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] {f : X ‚Üí Y}
  {s t U : Set X} {a : X}

/-! ### retrocompact sets -/

/-- A retrocompact set is a set whose intersection with every compact open is compact. -/
@[stacks 005A]
def IsRetrocompact (s : Set X) : Prop := ‚àÄ ‚¶ÉU‚¶Ñ, IsCompact U ‚Üí IsOpen U ‚Üí IsCompact (s ‚à© U)

@[simp] lemma IsRetrocompact.empty : IsRetrocompact (‚àÖ : Set X) := by simp [IsRetrocompact]
@[simp] lemma IsRetrocompact.univ : IsRetrocompact (univ : Set X) := by
  simp +contextual [IsRetrocompact]

@[simp] lemma IsRetrocompact.singleton : IsRetrocompact {a} :=
  fun _ _ _ ‚Ü¶ Subsingleton.singleton_inter.isCompact

lemma IsRetrocompact.union (hs : IsRetrocompact s) (ht : IsRetrocompact t) :
    IsRetrocompact (s ‚à™ t : Set X) :=
  fun _U hUcomp hUopen ‚Ü¶ union_inter_distrib_right .. ‚ñ∏ (hs hUcomp hUopen).union (ht hUcomp hUopen)

lemma IsRetrocompact.finsetSup {Œπ : Type*} {s : Finset Œπ} {t : Œπ ‚Üí Set X}
    (ht : ‚àÄ i ‚àà s, IsRetrocompact (t i)) : IsRetrocompact (s.sup t) := by
  induction' s using Finset.cons_induction with i s ih hi
  ¬∑ simp
  ¬∑ rw [Finset.sup_cons]
    exact (ht _ <| by simp).union <| hi <| Finset.forall_of_forall_cons ht

set_option linter.docPrime false in
lemma IsRetrocompact.finsetSup' {Œπ : Type*} {s : Finset Œπ} {hs} {t : Œπ ‚Üí Set X}
    (ht : ‚àÄ i ‚àà s, IsRetrocompact (t i)) : IsRetrocompact (s.sup' hs t) := by
  rw [Finset.sup'_eq_sup]; exact .finsetSup ht

lemma IsRetrocompact.inter [T2Space X] (hs : IsRetrocompact s) (ht : IsRetrocompact t) :
    IsRetrocompact (s ‚à© t : Set X) :=
  fun _U hUcomp hUopen ‚Ü¶ inter_inter_distrib_right .. ‚ñ∏ (hs hUcomp hUopen).inter (ht hUcomp hUopen)

lemma IsRetrocompact.finsetInf [T2Space X] {Œπ : Type*} {s : Finset Œπ} {t : Œπ ‚Üí Set X}
    (ht : ‚àÄ i ‚àà s, IsRetrocompact (t i)) : IsRetrocompact (s.inf t) := by
  induction' s using Finset.cons_induction with i s ih hi
  ¬∑ simp
  ¬∑ rw [Finset.inf_cons]
    exact (ht _ <| by simp).inter <| hi <| Finset.forall_of_forall_cons ht

set_option linter.docPrime false in
lemma IsRetrocompact.finsetInf' [T2Space X] {Œπ : Type*} {s : Finset Œπ} {hs} {t : Œπ ‚Üí Set X}
    (ht : ‚àÄ i ‚àà s, IsRetrocompact (t i)) : IsRetrocompact (s.inf' hs t) := by
  rw [Finset.inf'_eq_inf]; exact .finsetInf ht

lemma IsRetrocompact.inter_isOpen (hs : IsRetrocompact s) (ht : IsRetrocompact t)
    (htopen : IsOpen t) : IsRetrocompact (s ‚à© t : Set X) :=
  fun _U hUcomp hUopen ‚Ü¶ inter_assoc .. ‚ñ∏ hs (ht hUcomp hUopen) (htopen.inter hUopen)

lemma IsRetrocompact.isOpen_inter (hs : IsRetrocompact s) (ht : IsRetrocompact t)
    (hsopen : IsOpen s) : IsRetrocompact (s ‚à© t : Set X) :=
  inter_comm .. ‚ñ∏ ht.inter_isOpen hs hsopen

lemma IsRetrocompact_iff_isSpectralMap_subtypeVal :
    IsRetrocompact s ‚Üî IsSpectralMap (Subtype.val : s ‚Üí X) := by
  refine ‚ü®fun hs ‚Ü¶ ‚ü®continuous_subtype_val, fun t htopen htcomp ‚Ü¶ ?_‚ü©, fun hs t htcomp htopen ‚Ü¶ ?_‚ü©
  ¬∑ rw [IsEmbedding.subtypeVal.isCompact_iff, image_preimage_eq_inter_range,
      Subtype.range_coe_subtype, setOf_mem_eq, inter_comm]
    exact hs htcomp htopen
  ¬∑ simpa using (hs.isCompact_preimage_of_isOpen htopen htcomp).image continuous_subtype_val

@[stacks 005B]
lemma IsRetrocompact.image_of_isEmbedding (hs : IsRetrocompact s) (hfemb : IsEmbedding f)
    (hfcomp : IsRetrocompact (range f)) : IsRetrocompact (f '' s) := by
  rintro U hUcomp hUopen
  rw [‚Üê image_inter_preimage, ‚Üê hfemb.isCompact_iff]
  refine hs ?_ <| hUopen.preimage hfemb.continuous
  rw [hfemb.isCompact_iff, image_preimage_eq_inter_range, inter_comm]
  exact hfcomp hUcomp hUopen

@[stacks 005J] -- Extracted from the proof of `005J`
lemma IsRetrocompact.preimage_of_isOpenEmbedding {s : Set Y} (hf : IsOpenEmbedding f)
    (hs : IsRetrocompact s) : IsRetrocompact (f ‚Åª¬π' s) := by
  rintro U hUcomp hUopen
  rw [hf.isCompact_iff, image_preimage_inter]
  exact hs (hUcomp.image hf.continuous) <| hf.isOpenMap _ hUopen

@[stacks 09YE] -- Extracted from the proof of `09YE`
lemma IsRetrocompact.preimage_of_isClosedEmbedding {s : Set Y} (hf : IsClosedEmbedding f)
    (hf' : IsCompact (range f)·∂ú) (hs : IsRetrocompact s) : IsRetrocompact (f ‚Åª¬π' s) := by
  rintro U hUcomp hUopen
  have hfUopen : IsOpen (f '' U ‚à™ (range f)·∂ú) := by
    simpa [‚Üê range_diff_image hf.injective, sdiff_eq, compl_inter, union_comm]
      using (hf.isClosedMap _ hUopen.isClosed_compl).isOpen_compl
  have hfUcomp : IsCompact (f '' U ‚à™ (range f)·∂ú) := (hUcomp.image hf.continuous).union hf'
  simpa [inter_union_distrib_left, inter_left_comm, inter_eq_right.2 (image_subset_range ..),
    hf.isCompact_iff, image_preimage_inter] using (hs hfUcomp hfUopen).inter_left hf.isClosed_range

/-! ### Constructible sets -/

namespace Topology

/-- A constructible set is a set that can be written as the
finite union/finite intersection/complement of open retrocompact sets.

In other words, constructible sets form the boolean subalgebra generated by open retrocompact sets.
-/
def IsConstructible (s : Set X) : Prop :=
  s ‚àà BooleanSubalgebra.closure {U | IsOpen U ‚àß IsRetrocompact U}

@[simp]
protected lemma IsConstructible.empty : IsConstructible (‚àÖ : Set X) := BooleanSubalgebra.bot_mem

@[simp]
protected lemma IsConstructible.univ : IsConstructible (univ : Set X) := BooleanSubalgebra.top_mem

lemma IsConstructible.union : IsConstructible s ‚Üí IsConstructible t ‚Üí IsConstructible (s ‚à™ t) :=
  BooleanSubalgebra.sup_mem

lemma IsConstructible.inter : IsConstructible s ‚Üí IsConstructible t ‚Üí IsConstructible (s ‚à© t) :=
  BooleanSubalgebra.inf_mem

lemma IsConstructible.sdiff : IsConstructible s ‚Üí IsConstructible t ‚Üí IsConstructible (s \ t) :=
  BooleanSubalgebra.sdiff_mem

lemma IsConstructible.himp : IsConstructible s ‚Üí IsConstructible t ‚Üí IsConstructible (s ‚á® t) :=
  BooleanSubalgebra.himp_mem

@[simp] lemma isConstructible_compl : IsConstructible s·∂ú ‚Üî IsConstructible s :=
  BooleanSubalgebra.compl_mem_iff

alias ‚ü®IsConstructible.of_compl, IsConstructible.compl‚ü© := isConstructible_compl

lemma IsConstructible.iUnion [Finite Œπ] {f : Œπ ‚Üí Set X} (hf : ‚àÄ i, IsConstructible (f i)) :
    IsConstructible (‚ãÉ i, f i) := BooleanSubalgebra.iSup_mem hf

lemma IsConstructible.iInter [Finite Œπ] {f : Œπ ‚Üí Set X} (hf : ‚àÄ i, IsConstructible (f i)) :
    IsConstructible (‚ãÇ i, f i) := BooleanSubalgebra.iInf_mem hf

lemma IsConstructible.sUnion {S : Set (Set X)} (hS : S.Finite) (hS' : ‚àÄ s ‚àà S, IsConstructible s) :
    IsConstructible (‚ãÉ‚ÇÄ S) := BooleanSubalgebra.sSup_mem hS hS'

lemma IsConstructible.sInter {S : Set (Set X)} (hS : S.Finite) (hS' : ‚àÄ s ‚àà S, IsConstructible s) :
    IsConstructible (‚ãÇ‚ÇÄ S) := BooleanSubalgebra.sInf_mem hS hS'

lemma IsConstructible.biUnion {Œπ : Type*} {f : Œπ ‚Üí Set X} {t : Set Œπ} (ht : t.Finite)
    (hf : ‚àÄ i ‚àà t, IsConstructible (f i)) : IsConstructible (‚ãÉ i ‚àà t, f i) :=
  BooleanSubalgebra.biSup_mem ht hf

lemma IsConstructible.biInter {Œπ : Type*} {f : Œπ ‚Üí Set X} {t : Set Œπ} (ht : t.Finite)
    (hf : ‚àÄ i ‚àà t, IsConstructible (f i)) : IsConstructible (‚ãÇ i ‚àà t, f i) :=
  BooleanSubalgebra.biInf_mem ht hf

lemma _root_.IsRetrocompact.isConstructible (hUopen : IsOpen U) (hUcomp : IsRetrocompact U) :
    IsConstructible U := BooleanSubalgebra.subset_closure ‚ü®hUopen, hUcomp‚ü©

/-- An induction principle for constructible sets. If `p` holds for all open retrocompact
sets, and is preserved under union and complement, then `p` holds for all constructible sets. -/
@[elab_as_elim]
lemma IsConstructible.empty_union_induction {p : ‚àÄ s : Set X, IsConstructible s ‚Üí Prop}
    (open_retrocompact : ‚àÄ U (hUopen : IsOpen U) (hUcomp : IsRetrocompact U),
      p U (BooleanSubalgebra.subset_closure ‚ü®hUopen, hUcomp‚ü©))
    (union : ‚àÄ s hs t ht, p s hs ‚Üí p t ht ‚Üí p (s ‚à™ t) (hs.union ht))
    (compl : ‚àÄ s hs, p s hs ‚Üí p s·∂ú hs.compl) {s} (hs : IsConstructible s) : p s hs := by
  induction hs using BooleanSubalgebra.closure_bot_sup_induction with
  | mem U hU => exact open_retrocompact _ hU.1 hU.2
  | bot => exact open_retrocompact _ isOpen_empty .empty
  | sup s hs t ht hs' ht' => exact union _ _ _ _ hs' ht'
  | compl s hs hs' => exact compl _ _ hs'

/-- If `f` is continuous and is such that preimages of retrocompact sets are retrocompact, then
preimages of constructible sets are constructible. -/
@[stacks 005I]
lemma IsConstructible.preimage {s : Set Y} (hfcont : Continuous f)
    (hf : ‚àÄ s, IsRetrocompact s ‚Üí IsRetrocompact (f ‚Åª¬π' s)) (hs : IsConstructible s) :
    IsConstructible (f ‚Åª¬π' s) := by
  induction hs using IsConstructible.empty_union_induction with
  | open_retrocompact U hUopen hUcomp =>
    exact (hf _ hUcomp).isConstructible <| hUopen.preimage hfcont
  | union s hs t ht hs' ht' => rw [preimage_union]; exact hs'.union ht'
  | compl s hs hs' => rw [preimage_compl]; exact hs'.compl

@[stacks 005J]
lemma IsConstructible.preimage_of_isOpenEmbedding {s : Set Y} (hf : IsOpenEmbedding f)
    (hs : IsConstructible s) : IsConstructible (f ‚Åª¬π' s) :=
  hs.preimage hf.continuous fun _t ht ‚Ü¶ ht.preimage_of_isOpenEmbedding hf

@[stacks 09YE]
lemma IsConstructible.preimage_of_isClosedEmbedding {s : Set Y} (hf : IsClosedEmbedding f)
    (hf' : IsCompact (range f)·∂ú) (hs : IsConstructible s) : IsConstructible (f ‚Åª¬π' s) :=
  hs.preimage hf.continuous fun _t ht ‚Ü¶ ht.preimage_of_isClosedEmbedding hf hf'

@[stacks 09YD]
lemma IsConstructible.image_of_isOpenEmbedding (hfopen : IsOpenEmbedding f)
    (hfcomp : IsRetrocompact (range f)) (hs : IsConstructible s) : IsConstructible (f '' s) := by
  induction hs using IsConstructible.empty_union_induction with
  | open_retrocompact U hUopen hUcomp =>
    exact (hUcomp.image_of_isEmbedding hfopen.isEmbedding hfcomp).isConstructible <|
      hfopen.isOpenMap _ hUopen
  | union s hs t ht hs' ht' => rw [image_union]; exact hs'.union ht'
  | compl s hs hs' =>
    rw [‚Üê range_diff_image hfopen.injective]
    exact (hfcomp.isConstructible hfopen.isOpen_range).sdiff hs'

@[stacks 09YG]
lemma IsConstructible.image_of_isClosedEmbedding (hf : IsClosedEmbedding f)
    (hfcomp : IsRetrocompact (range f)·∂ú) (hs : IsConstructible s) : IsConstructible (f '' s) := by
  induction hs using IsConstructible.empty_union_induction with
  | open_retrocompact U hUopen hUcomp =>
    have hfU : IsOpen (f '' U ‚à™ (range f)·∂ú) := by
      simpa [‚Üê range_diff_image hf.injective, sdiff_eq, compl_inter, union_comm]
        using (hf.isClosedMap _ hUopen.isClosed_compl).isOpen_compl
    suffices h : IsRetrocompact (f '' U ‚à™ (range f)·∂ú) by
      simpa [union_inter_distrib_right, inter_eq_left.2 (image_subset_range ..)]
        using (h.isConstructible hfU).sdiff (hfcomp.isConstructible hf.isClosed_range.isOpen_compl)
    rintro V hVcomp hVopen
    rw [union_inter_distrib_right, ‚Üê image_inter_preimage]
    exact ((hUcomp (hf.isCompact_preimage hVcomp) (hVopen.preimage hf.continuous)).image
      hf.continuous).union <| hfcomp hVcomp hVopen
  | union s hs t ht hs' ht' => rw [image_union]; exact hs'.union ht'
  | compl s hs hs' =>
    rw [‚Üê range_diff_image hf.injective]
    exact (hfcomp.isConstructible hf.isClosed_range.isOpen_compl).of_compl.sdiff hs'

section CompactSpace
variable [CompactSpace X] {P : ‚àÄ s : Set X, IsConstructible s ‚Üí Prop} {B : Set (Set X)}

lemma _root_.IsRetrocompact.isCompact (hs : IsRetrocompact s) : IsCompact s := by
  simpa using hs CompactSpace.isCompact_univ

lemma _root_.TopologicalSpace.IsTopologicalBasis.IsRetrocompact_iff_isCompact
    (basis : IsTopologicalBasis B) (compact_inter : ‚àÄ U ‚àà B, ‚àÄ V ‚àà B, IsCompact (U ‚à© V))
    (hU : IsOpen U) : IsRetrocompact U ‚Üî IsCompact U := by
  refine ‚ü®IsRetrocompact.isCompact, fun hU' {V} hV' hV ‚Ü¶ ?_‚ü©
  obtain ‚ü®s, rfl‚ü© := eq_sUnion_finset_of_isTopologicalBasis_of_isCompact_open _ basis _ hU' hU
  obtain ‚ü®t, rfl‚ü© := eq_sUnion_finset_of_isTopologicalBasis_of_isCompact_open _ basis _ hV' hV
  rw [Set.sUnion_inter_sUnion]
  refine ((s.finite_toSet.image _).prod (t.finite_toSet.image _)).isCompact_biUnion ?_
  simp only [mem_prod, mem_image, Finset.mem_coe, Subtype.exists, exists_and_right, exists_eq_right,
    and_imp, forall_exists_index, Prod.forall]
  exact fun u v hu _ hv _ ‚Ü¶ compact_inter _ hu _ hv

lemma _root_.TopologicalSpace.IsTopologicalBasis.IsRetrocompact (basis : IsTopologicalBasis B)
    (compact_inter : ‚àÄ U ‚àà B, ‚àÄ V ‚àà B, IsCompact (U ‚à© V)) (hU : U ‚àà B) : IsRetrocompact U :=
  (basis.IsRetrocompact_iff_isCompact compact_inter <| basis.isOpen hU).2 <| by
    simpa using compact_inter _ hU _ hU

lemma _root_.TopologicalSpace.IsTopologicalBasis.isConstructible (basis : IsTopologicalBasis B)
    (compact_inter : ‚àÄ U ‚àà B, ‚àÄ V ‚àà B, IsCompact (U ‚à© V)) (hU : U ‚àà B) : IsConstructible U :=
  (basis.IsRetrocompact compact_inter hU).isConstructible <| basis.isOpen hU

proof_wanted IsConstructible.induction_of_isTopologicalBasis (basis : IsTopologicalBasis B)
    (compact_inter : ‚àÄ U ‚àà B, ‚àÄ V ‚àà B, IsCompact (U ‚à© V))
    (sdiff : ‚àÄ U (hU : U ‚àà B) ùí± (hùí±B : ùí± ‚äÜ B) (hùí± : ùí±.Finite), P (U \ ‚ãÉ V ‚àà ùí±, V)
      ((basis.isConstructible compact_inter hU).sdiff <| .biUnion hùí± fun V hV ‚Ü¶
        basis.isConstructible compact_inter <| hùí±B hV))
    (union : ‚àÄ s hs t ht, P s hs ‚Üí P t ht ‚Üí P (s ‚à™ t) (hs.union ht))
    (s : Set X) (hs : IsConstructible s) : P s hs

end CompactSpace

/-! ### Locally constructible sets -/

/-- A set in a topological space is locally constructible, if every point has a neighborhood on
which the set is constructible. -/
@[stacks 005G]
def IsLocallyConstructible (s : Set X) : Prop := ‚àÄ x, ‚àÉ U ‚àà ùìù x, IsOpen U ‚àß IsConstructible (s ‚à© U)

lemma IsConstructible.isLocallyConstructible (hs : IsConstructible s) : IsLocallyConstructible s :=
  fun _ ‚Ü¶ ‚ü®univ, by simpa‚ü©

lemma _root_.IsRetrocompact.isLocallyConstructible (hUopen : IsOpen U) (hUcomp : IsRetrocompact U) :
    IsLocallyConstructible U := (hUcomp.isConstructible hUopen).isLocallyConstructible

@[simp] protected lemma IsLocallyConstructible.empty : IsLocallyConstructible (‚àÖ : Set X) :=
  IsConstructible.empty.isLocallyConstructible

@[simp] protected lemma IsLocallyConstructible.univ : IsLocallyConstructible (univ : Set X) :=
  IsConstructible.univ.isLocallyConstructible

lemma IsLocallyConstructible.inter (hs : IsLocallyConstructible s) (ht : IsLocallyConstructible t) :
    IsLocallyConstructible (s ‚à© t) := by
  rintro x
  obtain ‚ü®U, hxU, hU, hsU‚ü© := hs x
  obtain ‚ü®V, hxV, hV, htV‚ü© := ht x
  exact ‚ü®U ‚à© V, Filter.inter_mem hxU hxV, hU.inter hV, inter_inter_inter_comm .. ‚ñ∏ hsU.inter htV‚ü©

lemma IsLocallyConstructible.finsetInf {Œπ : Type*} {s : Finset Œπ} {t : Œπ ‚Üí Set X}
    (ht : ‚àÄ i ‚àà s, IsLocallyConstructible (t i)) : IsLocallyConstructible (s.inf t) := by
  induction' s using Finset.cons_induction with i s ih hi
  ¬∑ simp
  ¬∑ rw [Finset.inf_cons]
    exact (ht _ <| by simp).inter <| hi <| Finset.forall_of_forall_cons ht

set_option linter.docPrime false in
lemma IsLocallyConstructible.finsetInf' {Œπ : Type*} {s : Finset Œπ} {hs} {t : Œπ ‚Üí Set X}
    (ht : ‚àÄ i ‚àà s, IsLocallyConstructible (t i)) : IsLocallyConstructible (s.inf' hs t) := by
  rw [Finset.inf'_eq_inf]; exact .finsetInf ht

private lemma infClosed_isLocallyConstructible : InfClosed {s : Set X | IsLocallyConstructible s} :=
  fun _s hs _t ht ‚Ü¶ hs.inter ht

lemma IsLocallyConstructible.iInter [Finite Œπ] {f : Œπ ‚Üí Set X}
    (hf : ‚àÄ i, IsLocallyConstructible (f i)) : IsLocallyConstructible (‚ãÇ i, f i) :=
  infClosed_isLocallyConstructible.iInf_mem .univ hf

lemma IsLocallyConstructible.sInter {S : Set (Set X)} (hS : S.Finite)
    (hS' : ‚àÄ s ‚àà S, IsLocallyConstructible s) : IsLocallyConstructible (‚ãÇ‚ÇÄ S) :=
  infClosed_isLocallyConstructible.sInf_mem hS .univ hS'

end Topology
