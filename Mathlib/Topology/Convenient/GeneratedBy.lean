/-
Copyright (c) 2025 Joël Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joël Riou, Dagur Asgeirsson, Ben Eltschig
-/
import Mathlib.Topology.ContinuousMap.Basic

/-!
# The topology that is generated by a family of topological spaces

Let `X : ι → Type u` be a family of topological spaces.
Let `Y` be a topological space. We introduce a type synonym
`WithGeneratedByTopology X Y` for `Y`. This type is endowed
with the `X`-generated topology, which is coinduced by
all continuous maps `X i → Y`. When the bijection
`WithGeneratedByTopology X Y ≃ Y` is an homeomorphism,
we say that `Y` is `X`-generated (typeclass `IsGeneratedBy X Y`).

## TODO (@joelriou)
* Redefine compactly generated spaces and delta generated spaces using
these definitions
* Define the category of `X`-generated spaces and show that it is coreflective in `TopCat`
* Show that under suitable assumptions, the category of `X`-generated
spaces is a closed cartesian monoidal category.

## References
* [Rainer M. Vogt, *Convenient categories of topological spaces
for homotopy theory*][vogt-1971]
* [Martín Escardó, Jimmie Lawson and Alex Simpson, *Comparing Cartesian closed
categories of (core) compactly generated spaces*][escardo-lawson-simpson-2004]

-/

universe v v' t u

open Topology

variable {ι : Type t} (X : ι → Type u) [∀ i, TopologicalSpace (X i)]
  {Y : Type v} [tY : TopologicalSpace Y] {Z : Type v'} [TopologicalSpace Z]

namespace TopologicalSpace

/-- Given a family of topological spaces `X i`, the `X`-generated topology on
a topological space `Y` is the topology that is coinduced
by all continuous maps `X i → Y`. -/
def generatedBy : TopologicalSpace Y :=
  ⨆ (i : ι) (f : C(X i, Y)), coinduced f inferInstance

end TopologicalSpace

variable {X}

namespace Topology

/-- Given a family of topological spaces `X i`, and a topological space `Y`,
this is a type synonym for `Y` which we endow with the `X`-generated topology. -/
@[nolint unusedArguments]
def WithGeneratedByTopology (X : ι → Type u) [∀ i, TopologicalSpace (X i)]
    (Y : Type v) [TopologicalSpace Y] := Y

namespace WithGeneratedByTopology

/-- The obvious bijection `WithGeneratedByTopology X Y ≃ Y`, where
the source is endowed with the `X`-generated topology. See `continuous_equiv`
for the continuity of `equiv`. The inverse map `equiv.symm` is continuous
iff `Y` is `X`-generated, see `isGeneratedBy_iff`. -/
def equiv : WithGeneratedByTopology X Y ≃ Y := Equiv.refl _

instance {Y : Type v} [TopologicalSpace Y] :
    TopologicalSpace (WithGeneratedByTopology X Y) :=
  .generatedBy X (Y := Y)

lemma isOpen_iff {U : Set (WithGeneratedByTopology X Y)} :
    IsOpen U ↔ ∀ ⦃i : ι⦄ (f : C(X i, Y)), IsOpen (f ⁻¹' (equiv.symm ⁻¹' U)) := by
  simp [isOpen_iSup_iff, isOpen_coinduced, equiv, Equiv.refl]

lemma isClosed_iff {U : Set (WithGeneratedByTopology X Y)} :
    IsClosed U ↔ ∀ ⦃i : ι⦄ (f : C(X i, Y)), IsClosed (f ⁻¹' (equiv.symm ⁻¹' U)) := by
  simp [isClosed_iSup_iff, isClosed_coinduced, equiv, Equiv.refl]

lemma continuous_from_iff (g : WithGeneratedByTopology X Y → Z) :
    Continuous g ↔ ∀ ⦃i : ι⦄ (f : C(X i, Y)), Continuous (g ∘ equiv.symm ∘ f : X i → Z) := by
  simp only [continuous_iSup_dom]
  exact forall_congr' (fun i ↦ forall_congr'
    (fun f ↦ (by rw [continuous_coinduced_dom]; simp [equiv, Equiv.refl])))

@[continuity, fun_prop]
lemma continuous_equiv : Continuous (equiv (X := X) (Y := Y)) := by
  rw [continuous_def]
  intro U hU
  rw [isOpen_iff]
  intro i f
  exact f.continuous.isOpen_preimage _ hU

end WithGeneratedByTopology

end Topology

variable {tY} in
lemma TopologicalSpace.generatedBy_le : generatedBy X ≤ tY :=
  fun U hU ↦ WithGeneratedByTopology.continuous_equiv.isOpen_preimage U hU

omit tY in
lemma TopologicalSpace.generatedBy_mono {t₁ t₂ : TopologicalSpace Y} (h : t₁ ≤ t₂) :
    t₁.generatedBy X ≤ t₂.generatedBy X :=
  iSup₂_le fun i f ↦ le_iSup₂_of_le i ⟨f, continuous_le_rng h @f.2⟩ le_rfl

namespace Topology

variable (X Y) in
/-- Given a family of topological spaces `X i`, we say that a topological space is
`X`-generated (`IsGeneratedBy X Y`) when the topology on `Y` is the `X`-generated
topology, i.e. when the identity is an homeomorphism
`WithGeneratedByTopology X Y ≃ₜ Y` (see `IsGeneratedBy.homeomorph`). -/
@[mk_iff]
class IsGeneratedBy : Prop where
  continuous_equiv_symm : Continuous (WithGeneratedByTopology.equiv (X := X) (Y := Y)).symm

namespace IsGeneratedBy

attribute [continuity] continuous_equiv_symm

section

variable {tY}

lemma le_generatedBy [IsGeneratedBy X Y] : tY ≤ .generatedBy X :=
  fun U hU ↦ continuous_equiv_symm.isOpen_preimage U hU

lemma generatedBy_eq [IsGeneratedBy X Y] : .generatedBy X = tY :=
  le_antisymm TopologicalSpace.generatedBy_le le_generatedBy

lemma iff_le_generatedBy :
    IsGeneratedBy X Y ↔ tY ≤ .generatedBy X :=
  ⟨fun _ ↦ le_generatedBy, fun h ↦ ⟨by rwa [continuous_def]⟩⟩

end

section

variable [IsGeneratedBy X Y]

/-- The homeomorphism `WithGeneratedByTopology X Y ≃ₜ Y` when `Y` is `X`-generated. -/
def homeomorph [IsGeneratedBy X Y] : WithGeneratedByTopology X Y ≃ₜ Y where
  toEquiv := WithGeneratedByTopology.equiv
  continuous_toFun := by continuity
  continuous_invFun := by continuity

@[simp]
lemma homeomorph_coe :
   ⇑(homeomorph (X := X) (Y := Y)) = WithGeneratedByTopology.equiv := rfl

@[simp]
lemma homeomorph_symm_coe :
   ⇑(homeomorph (X := X) (Y := Y)).symm = WithGeneratedByTopology.equiv.symm := rfl

variable (X)

lemma isOpen_iff {U : Set Y} :
    IsOpen U ↔ ∀ ⦃i : ι⦄ (f : C(X i, Y)), IsOpen (f ⁻¹' U) := by
  simp [← (homeomorph (X := X)).isQuotientMap.isOpen_preimage,
    WithGeneratedByTopology.isOpen_iff]

lemma isClosed_iff {U : Set Y} :
    IsClosed U ↔ ∀ ⦃i : ι⦄ (f : C(X i, Y)), IsClosed (f ⁻¹' U) := by
  simp [← (homeomorph (X := X)).isQuotientMap.isClosed_preimage,
    WithGeneratedByTopology.isClosed_iff]

lemma continuous_iff (g : Y → Z) :
    Continuous g ↔ ∀ ⦃i : ι⦄ (f : C(X i, Y)), Continuous (g ∘ f) := by
  rw [(homeomorph (X := X)).isQuotientMap.continuous_iff,
    WithGeneratedByTopology.continuous_from_iff]
  rfl

lemma equiv_symm_comp_continuous_iff (g : Y → Z) :
    Continuous ((WithGeneratedByTopology.equiv (X := X)).symm ∘ g) ↔ Continuous g := by
  refine ⟨fun hg ↦ WithGeneratedByTopology.continuous_equiv.comp hg, fun hg ↦ ?_⟩
  rw [continuous_iff (X := X)]
  intro i f
  rw [continuous_def]
  intro U hU
  rw [WithGeneratedByTopology.isOpen_iff] at hU
  exact hU (ContinuousMap.comp ⟨g, hg⟩ f)

end

instance (i : ι) : IsGeneratedBy X (X i) where
  continuous_equiv_symm := by
    rw [continuous_def]
    intro U hU
    rw [WithGeneratedByTopology.isOpen_iff] at hU
    exact hU ⟨_, continuous_id⟩

instance : IsGeneratedBy X (WithGeneratedByTopology X Y) where
  continuous_equiv_symm := by
    rw [continuous_def]
    intro U hU
    rw [WithGeneratedByTopology.isOpen_iff] at hU ⊢
    intro i f
    refine hU ⟨WithGeneratedByTopology.equiv.symm ∘ f, ?_⟩
    rw [continuous_iff X]
    intro j g
    rw [Function.comp_assoc, equiv_symm_comp_continuous_iff]
    continuity

instance : IsGeneratedBy X (PUnit.{v + 1}) := by
  rw [iff_le_generatedBy]
  exact Eq.le (by subsingleton)

omit [TopologicalSpace Z]
/-- Any topology coinduced by an `X`-generated topology is `X`-generated. -/
protected lemma coinduced [IsGeneratedBy X Y] (f : Y → Z) :
    IsGeneratedBy X (tY := tY.coinduced f) := by
  let _ := tY.coinduced f
  refine iff_le_generatedBy.2 ?_
  exact ((equiv_symm_comp_continuous_iff (X := X) f).2 continuous_coinduced_rng).coinduced_le

/-- Suprema of `X`-generated topologies are `X`-generated. -/
protected lemma iSup {Y : Type*} {κ : Sort*} {t : κ → TopologicalSpace Y}
    (h : ∀ k, IsGeneratedBy X (tY := t k)) : @IsGeneratedBy _ X _ Y (⨆ k, t k) :=
  iff_le_generatedBy.2 <| iSup_le_iff.2 fun k ↦
    (h k).le_generatedBy.trans <| TopologicalSpace.generatedBy_mono <| le_iSup t k

/-- Suprema of `X`-generated topologies are `X`-generated. -/
protected lemma sup {Y : Type*} {t₁ t₂ : TopologicalSpace Y}
    (h₁ : IsGeneratedBy X (tY := t₁)) (h₂ : @IsGeneratedBy _ X _ Y t₂) :
    IsGeneratedBy X (tY := t₁ ⊔ t₂) := by
  rw [sup_eq_iSup]
  exact .iSup <| Bool.forall_bool.2 ⟨h₂, h₁⟩

end IsGeneratedBy

lemma IsQuotientMap.isGeneratedBy {f : Y → Z} (hf : IsQuotientMap f) [IsGeneratedBy X Y] :
    IsGeneratedBy X Z :=
  hf.eq_coinduced ▸ IsGeneratedBy.coinduced f

end Topology

/-- Quotients of `X`-generated spaces are `X`-generated. -/
instance Quot.isGeneratedBy [IsGeneratedBy X Y] {r : Y → Y → Prop} :
    IsGeneratedBy X (Quot r) :=
  isQuotientMap_quot_mk.isGeneratedBy

/-- Quotients of `X`-generated spaces are `X`-generated. -/
instance Quotient.isGeneratedBy [IsGeneratedBy X Y] {s : Setoid Y} :
    IsGeneratedBy X (Quotient s) :=
  isQuotientMap_quotient_mk'.isGeneratedBy

/-- Disjoint unions of `X`-generated spaces are `X`-generated. -/
instance Sum.isGeneratedBy [IsGeneratedBy X Y] [IsGeneratedBy X Z] :
    IsGeneratedBy X (Y ⊕ Z) :=
  IsGeneratedBy.sup (.coinduced Sum.inl) (.coinduced Sum.inr)

/-- Disjoint unions of `X`-generated spaces are `X`-generated. -/
instance Sigma.isGeneratedBy {κ : Type*} {Y : κ → Type*} [∀ k, TopologicalSpace (Y k)]
    [∀ k, IsGeneratedBy X (Y k)] : IsGeneratedBy X (Σ k, Y k) :=
  .iSup fun _ ↦ .coinduced _
