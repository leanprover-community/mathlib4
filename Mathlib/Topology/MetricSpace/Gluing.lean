/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Order.ConditionallyCompleteLattice.Group
import Mathlib.Topology.MetricSpace.Isometry

/-!
# Metric space gluing

Gluing two metric spaces along a common subset. Formally, we are given

```
     Œ¶
  Z ---> X
  |
  |Œ®
  v
  Y
```
where `hŒ¶ : Isometry Œ¶` and `hŒ® : Isometry Œ®`.
We want to complete the square by a space `GlueSpacescan hŒ¶ hŒ®` and two isometries
`toGlueL hŒ¶ hŒ®` and `toGlueR hŒ¶ hŒ®` that make the square commute.
We start by defining a predistance on the disjoint union `X ‚äï Y`, for which
points `Œ¶ p` and `Œ® p` are at distance 0. The (quotient) metric space associated
to this predistance is the desired space.

This is an instance of a more general construction, where `Œ¶` and `Œ®` do not have to be isometries,
but the distances in the image almost coincide, up to `2Œµ` say. Then one can almost glue the two
spaces so that the images of a point under `Œ¶` and `Œ®` are `Œµ`-close. If `Œµ > 0`, this yields a
metric space structure on `X ‚äï Y`, without the need to take a quotient. In particular,
this gives a natural metric space structure on `X ‚äï Y`, where the basepoints
are at distance 1, say, and the distances between other points are obtained by going through the two
basepoints.
(We also register the same metric space structure on a general disjoint union `Œ£ i, E i`).

We also define the inductive limit of metric spaces. Given
```
     f 0        f 1        f 2        f 3
X 0 -----> X 1 -----> X 2 -----> X 3 -----> ...
```
where the `X n` are metric spaces and `f n` isometric embeddings, we define the inductive
limit of the `X n`, also known as the increasing union of the `X n` in this context, if we
identify `X n` and `X (n+1)` through `f n`. This is a metric space in which all `X n` embed
isometrically and in a way compatible with `f n`.

-/

noncomputable section

universe u v w

open Function Set Uniformity Topology

namespace Metric

section ApproxGluing

variable {X : Type u} {Y : Type v} {Z : Type w}
variable [MetricSpace X] [MetricSpace Y] {Œ¶ : Z ‚Üí X} {Œ® : Z ‚Üí Y} {Œµ : ‚Ñù}

/-- Define a predistance on `X ‚äï Y`, for which `Œ¶ p` and `Œ® p` are at distance `Œµ` -/
def glueDist (Œ¶ : Z ‚Üí X) (Œ® : Z ‚Üí Y) (Œµ : ‚Ñù) : X ‚äï Y ‚Üí X ‚äï Y ‚Üí ‚Ñù
  | .inl x, .inl y => dist x y
  | .inr x, .inr y => dist x y
  | .inl x, .inr y => (‚®Ö p, dist x (Œ¶ p) + dist y (Œ® p)) + Œµ
  | .inr x, .inl y => (‚®Ö p, dist y (Œ¶ p) + dist x (Œ® p)) + Œµ

private theorem glueDist_self (Œ¶ : Z ‚Üí X) (Œ® : Z ‚Üí Y) (Œµ : ‚Ñù) : ‚àÄ x, glueDist Œ¶ Œ® Œµ x x = 0
  | .inl _ => dist_self _
  | .inr _ => dist_self _

theorem glueDist_glued_points [Nonempty Z] (Œ¶ : Z ‚Üí X) (Œ® : Z ‚Üí Y) (Œµ : ‚Ñù) (p : Z) :
    glueDist Œ¶ Œ® Œµ (.inl (Œ¶ p)) (.inr (Œ® p)) = Œµ := by
  have : ‚®Ö q, dist (Œ¶ p) (Œ¶ q) + dist (Œ® p) (Œ® q) = 0 := by
    have A : ‚àÄ q, 0 ‚â§ dist (Œ¶ p) (Œ¶ q) + dist (Œ® p) (Œ® q) := fun _ => by positivity
    refine le_antisymm ?_ (le_ciInf A)
    have : 0 = dist (Œ¶ p) (Œ¶ p) + dist (Œ® p) (Œ® p) := by simp
    rw [this]
    exact ciInf_le ‚ü®0, forall_mem_range.2 A‚ü© p
  simp only [glueDist, this, zero_add]

private theorem glueDist_comm (Œ¶ : Z ‚Üí X) (Œ® : Z ‚Üí Y) (Œµ : ‚Ñù) :
    ‚àÄ x y, glueDist Œ¶ Œ® Œµ x y = glueDist Œ¶ Œ® Œµ y x
  | .inl _, .inl _ => dist_comm _ _
  | .inr _, .inr _ => dist_comm _ _
  | .inl _, .inr _ => rfl
  | .inr _, .inl _ => rfl

theorem glueDist_swap (Œ¶ : Z ‚Üí X) (Œ® : Z ‚Üí Y) (Œµ : ‚Ñù) :
    ‚àÄ x y, glueDist Œ® Œ¶ Œµ x.swap y.swap = glueDist Œ¶ Œ® Œµ x y
  | .inl _, .inl _ => rfl
  | .inr _, .inr _ => rfl
  | .inl _, .inr _ => by simp only [glueDist, Sum.swap_inl, Sum.swap_inr, add_comm]
  | .inr _, .inl _ => by simp only [glueDist, Sum.swap_inl, Sum.swap_inr, add_comm]

theorem le_glueDist_inl_inr (Œ¶ : Z ‚Üí X) (Œ® : Z ‚Üí Y) (Œµ : ‚Ñù) (x y) :
    Œµ ‚â§ glueDist Œ¶ Œ® Œµ (.inl x) (.inr y) :=
  le_add_of_nonneg_left <| Real.iInf_nonneg fun _ => by positivity

theorem le_glueDist_inr_inl (Œ¶ : Z ‚Üí X) (Œ® : Z ‚Üí Y) (Œµ : ‚Ñù) (x y) :
    Œµ ‚â§ glueDist Œ¶ Œ® Œµ (.inr x) (.inl y) := by
  rw [glueDist_comm]; apply le_glueDist_inl_inr

section
variable [Nonempty Z]

private theorem glueDist_triangle_inl_inr_inr (Œ¶ : Z ‚Üí X) (Œ® : Z ‚Üí Y) (Œµ : ‚Ñù) (x : X) (y z : Y) :
    glueDist Œ¶ Œ® Œµ (.inl x) (.inr z) ‚â§
      glueDist Œ¶ Œ® Œµ (.inl x) (.inr y) + glueDist Œ¶ Œ® Œµ (.inr y) (.inr z) := by
  simp only [glueDist]
  rw [add_right_comm, add_le_add_iff_right]
  refine le_ciInf_add fun p => ciInf_le_of_le ‚ü®0, ?_‚ü© p ?_
  ¬∑ exact forall_mem_range.2 fun _ => by positivity
  ¬∑ linarith [dist_triangle_left z (Œ® p) y]

private theorem glueDist_triangle_inl_inr_inl (Œ¶ : Z ‚Üí X) (Œ® : Z ‚Üí Y) (Œµ : ‚Ñù)
    (H : ‚àÄ p q, |dist (Œ¶ p) (Œ¶ q) - dist (Œ® p) (Œ® q)| ‚â§ 2 * Œµ) (x : X) (y : Y) (z : X) :
    glueDist Œ¶ Œ® Œµ (.inl x) (.inl z) ‚â§
      glueDist Œ¶ Œ® Œµ (.inl x) (.inr y) + glueDist Œ¶ Œ® Œµ (.inr y) (.inl z) := by
  simp_rw [glueDist, add_add_add_comm _ Œµ, add_assoc]
  refine le_ciInf_add fun p => ?_
  rw [add_left_comm, add_assoc, ‚Üê two_mul]
  refine le_ciInf_add fun q => ?_
  rw [dist_comm z]
  linarith [dist_triangle4 x (Œ¶ p) (Œ¶ q) z, dist_triangle_left (Œ® p) (Œ® q) y, (abs_le.1 (H p q)).2]

private theorem glueDist_triangle (Œ¶ : Z ‚Üí X) (Œ® : Z ‚Üí Y) (Œµ : ‚Ñù)
    (H : ‚àÄ p q, |dist (Œ¶ p) (Œ¶ q) - dist (Œ® p) (Œ® q)| ‚â§ 2 * Œµ) :
    ‚àÄ x y z, glueDist Œ¶ Œ® Œµ x z ‚â§ glueDist Œ¶ Œ® Œµ x y + glueDist Œ¶ Œ® Œµ y z
  | .inl _, .inl _, .inl _ => dist_triangle _ _ _
  | .inr _, .inr _, .inr _ => dist_triangle _ _ _
  | .inr x, .inl y, .inl z => by
    simp only [‚Üê glueDist_swap Œ¶]
    apply glueDist_triangle_inl_inr_inr
  | .inr x, .inr y, .inl z => by
    simpa only [glueDist_comm, add_comm] using glueDist_triangle_inl_inr_inr _ _ _ z y x
  | .inl x, .inl y, .inr z => by
    simpa only [‚Üê glueDist_swap Œ¶, glueDist_comm, add_comm, Sum.swap_inl, Sum.swap_inr]
      using glueDist_triangle_inl_inr_inr Œ® Œ¶ Œµ z y x
  | .inl _, .inr _, .inr _ => glueDist_triangle_inl_inr_inr ..
  | .inl x, .inr y, .inl z => glueDist_triangle_inl_inr_inl Œ¶ Œ® Œµ H x y z
  | .inr x, .inl y, .inr z => by
    simp only [‚Üê glueDist_swap Œ¶]
    apply glueDist_triangle_inl_inr_inl
    simpa only [abs_sub_comm]

end

private theorem eq_of_glueDist_eq_zero (Œ¶ : Z ‚Üí X) (Œ® : Z ‚Üí Y) (Œµ : ‚Ñù) (Œµ0 : 0 < Œµ) :
    ‚àÄ p q : X ‚äï Y, glueDist Œ¶ Œ® Œµ p q = 0 ‚Üí p = q
  | .inl x, .inl y, h => by rw [eq_of_dist_eq_zero h]
  | .inl x, .inr y, h => by exfalso; linarith [le_glueDist_inl_inr Œ¶ Œ® Œµ x y]
  | .inr x, .inl y, h => by exfalso; linarith [le_glueDist_inr_inl Œ¶ Œ® Œµ x y]
  | .inr x, .inr y, h => by rw [eq_of_dist_eq_zero h]

theorem Sum.mem_uniformity_iff_glueDist (hŒµ : 0 < Œµ) (s : Set ((X ‚äï Y) √ó (X ‚äï Y))) :
    s ‚àà ùì§ (X ‚äï Y) ‚Üî ‚àÉ Œ¥ > 0, ‚àÄ a b, glueDist Œ¶ Œ® Œµ a b < Œ¥ ‚Üí (a, b) ‚àà s := by
  simp only [Sum.uniformity, Filter.mem_sup, Filter.mem_map, mem_uniformity_dist, mem_preimage]
  constructor
  ¬∑ rintro ‚ü®‚ü®Œ¥X, Œ¥X0, hX‚ü©, Œ¥Y, Œ¥Y0, hY‚ü©
    refine ‚ü®min (min Œ¥X Œ¥Y) Œµ, lt_min (lt_min Œ¥X0 Œ¥Y0) hŒµ, ?_‚ü©
    rintro (a | a) (b | b) h <;> simp only [lt_min_iff] at h
    ¬∑ exact hX h.1.1
    ¬∑ exact absurd h.2 (le_glueDist_inl_inr _ _ _ _ _).not_gt
    ¬∑ exact absurd h.2 (le_glueDist_inr_inl _ _ _ _ _).not_gt
    ¬∑ exact hY h.1.2
  ¬∑ rintro ‚ü®Œµ, Œµ0, H‚ü©
    constructor <;> exact ‚ü®Œµ, Œµ0, fun _ _ h => H _ _ h‚ü©

/-- Given two maps `Œ¶` and `Œ®` intro metric spaces `X` and `Y` such that the distances between
`Œ¶ p` and `Œ¶ q`, and between `Œ® p` and `Œ® q`, coincide up to `2 Œµ` where `Œµ > 0`, one can almost
glue the two spaces `X` and `Y` along the images of `Œ¶` and `Œ®`, so that `Œ¶ p` and `Œ® p` are
at distance `Œµ`. -/
def glueMetricApprox [Nonempty Z] (Œ¶ : Z ‚Üí X) (Œ® : Z ‚Üí Y) (Œµ : ‚Ñù) (Œµ0 : 0 < Œµ)
    (H : ‚àÄ p q, |dist (Œ¶ p) (Œ¶ q) - dist (Œ® p) (Œ® q)| ‚â§ 2 * Œµ) : MetricSpace (X ‚äï Y) where
  dist := glueDist Œ¶ Œ® Œµ
  dist_self := glueDist_self Œ¶ Œ® Œµ
  dist_comm := glueDist_comm Œ¶ Œ® Œµ
  dist_triangle := glueDist_triangle Œ¶ Œ® Œµ H
  eq_of_dist_eq_zero := eq_of_glueDist_eq_zero Œ¶ Œ® Œµ Œµ0 _ _
  toUniformSpace := Sum.instUniformSpace
  uniformity_dist := uniformity_dist_of_mem_uniformity _ _ <| Sum.mem_uniformity_iff_glueDist Œµ0

end ApproxGluing

section Sum

/-!
### Metric on `X ‚äï Y`

A particular case of the previous construction is when one uses basepoints in `X` and `Y` and one
glues only along the basepoints, putting them at distance 1. We give a direct definition of
the distance, without `iInf`, as it is easier to use in applications, and show that it is equal to
the gluing distance defined above to take advantage of the lemmas we have already proved.
-/
variable {X : Type u} {Y : Type v} {Z : Type w}
variable [MetricSpace X] [MetricSpace Y]

/-- Distance on a disjoint union. There are many (noncanonical) ways to put a distance compatible
with each factor.
If the two spaces are bounded, one can say for instance that each point in the first is at distance
`diam X + diam Y + 1` of each point in the second.
Instead, we choose a construction that works for unbounded spaces, but requires basepoints,
chosen arbitrarily.
We embed isometrically each factor, set the basepoints at distance 1,
arbitrarily, and say that the distance from `a` to `b` is the sum of the distances of `a` and `b` to
their respective basepoints, plus the distance 1 between the basepoints.
Since there is an arbitrary choice in this construction, it is not an instance by default. -/
protected def Sum.dist : X ‚äï Y ‚Üí X ‚äï Y ‚Üí ‚Ñù
  | .inl a, .inl a' => dist a a'
  | .inr b, .inr b' => dist b b'
  | .inl a, .inr b => dist a (Nonempty.some ‚ü®a‚ü©) + 1 + dist (Nonempty.some ‚ü®b‚ü©) b
  | .inr b, .inl a => dist b (Nonempty.some ‚ü®b‚ü©) + 1 + dist (Nonempty.some ‚ü®a‚ü©) a

theorem Sum.dist_eq_glueDist {p q : X ‚äï Y} (x : X) (y : Y) :
    Sum.dist p q =
      glueDist (fun _ : Unit => Nonempty.some ‚ü®x‚ü©) (fun _ : Unit => Nonempty.some ‚ü®y‚ü©) 1 p q := by
  cases p <;> cases q <;> first |rfl|simp [Sum.dist, glueDist, dist_comm, add_comm,
    add_left_comm, add_assoc]

private theorem Sum.dist_comm (x y : X ‚äï Y) : Sum.dist x y = Sum.dist y x := by
  cases x <;> cases y <;> simp [Sum.dist, _root_.dist_comm, add_comm, add_left_comm]

theorem Sum.one_le_dist_inl_inr {x : X} {y : Y} : 1 ‚â§ Sum.dist (.inl x) (.inr y) := by
  grw [Sum.dist, ‚Üê le_add_of_nonneg_right dist_nonneg, ‚Üê le_add_of_nonneg_left dist_nonneg]

theorem Sum.one_le_dist_inr_inl {x : X} {y : Y} : 1 ‚â§ Sum.dist (.inr y) (.inl x) := by
  rw [Sum.dist_comm]; exact Sum.one_le_dist_inl_inr

private theorem Sum.mem_uniformity (s : Set ((X ‚äï Y) √ó (X ‚äï Y))) :
    s ‚àà ùì§ (X ‚äï Y) ‚Üî ‚àÉ Œµ > 0, ‚àÄ a b, Sum.dist a b < Œµ ‚Üí (a, b) ‚àà s := by
  constructor
  ¬∑ rintro ‚ü®hsX, hsY‚ü©
    rcases mem_uniformity_dist.1 hsX with ‚ü®ŒµX, ŒµX0, hX‚ü©
    rcases mem_uniformity_dist.1 hsY with ‚ü®ŒµY, ŒµY0, hY‚ü©
    refine ‚ü®min (min ŒµX ŒµY) 1, lt_min (lt_min ŒµX0 ŒµY0) zero_lt_one, ?_‚ü©
    rintro (a | a) (b | b) h
    ¬∑ exact hX (lt_of_lt_of_le h (le_trans (min_le_left _ _) (min_le_left _ _)))
    ¬∑ cases not_le_of_gt (lt_of_lt_of_le h (min_le_right _ _)) Sum.one_le_dist_inl_inr
    ¬∑ cases not_le_of_gt (lt_of_lt_of_le h (min_le_right _ _)) Sum.one_le_dist_inr_inl
    ¬∑ exact hY (lt_of_lt_of_le h (le_trans (min_le_left _ _) (min_le_right _ _)))
  ¬∑ rintro ‚ü®Œµ, Œµ0, H‚ü©
    constructor <;> rw [Filter.mem_map, mem_uniformity_dist] <;> exact ‚ü®Œµ, Œµ0, fun _ _ h => H _ _ h‚ü©

/-- The distance on the disjoint union indeed defines a metric space. All the distance properties
follow from our choice of the distance. The harder work is to show that the uniform structure
defined by the distance coincides with the disjoint union uniform structure. -/
def metricSpaceSum : MetricSpace (X ‚äï Y) where
  dist := Sum.dist
  dist_self x := by cases x <;> simp only [Sum.dist, dist_self]
  dist_comm := Sum.dist_comm
  dist_triangle
    | .inl p, .inl q, .inl r => dist_triangle p q r
    | .inl p, .inr q, _ => by
      simp only [Sum.dist_eq_glueDist p q]
      exact glueDist_triangle _ _ _ (by simp) _ _ _
    | _, .inl q, .inr r => by
      simp only [Sum.dist_eq_glueDist q r]
      exact glueDist_triangle _ _ _ (by simp) _ _ _
    | .inr p, _, .inl r => by
      simp only [Sum.dist_eq_glueDist r p]
      exact glueDist_triangle _ _ _ (by simp) _ _ _
    | .inr p, .inr q, .inr r => dist_triangle p q r
  eq_of_dist_eq_zero {p q} h := by
    rcases p with p | p <;> rcases q with q | q
    ¬∑ rw [eq_of_dist_eq_zero h]
    ¬∑ exact eq_of_glueDist_eq_zero _ _ _ one_pos _ _ ((Sum.dist_eq_glueDist p q).symm.trans h)
    ¬∑ exact eq_of_glueDist_eq_zero _ _ _ one_pos _ _ ((Sum.dist_eq_glueDist q p).symm.trans h)
    ¬∑ rw [eq_of_dist_eq_zero h]
  toUniformSpace := Sum.instUniformSpace
  uniformity_dist := uniformity_dist_of_mem_uniformity _ _ Sum.mem_uniformity

attribute [local instance] metricSpaceSum

theorem Sum.dist_eq {x y : X ‚äï Y} : dist x y = Sum.dist x y := rfl

/-- The left injection of a space in a disjoint union is an isometry -/
theorem isometry_inl : Isometry (Sum.inl : X ‚Üí X ‚äï Y) :=
  Isometry.of_dist_eq fun _ _ => rfl

/-- The right injection of a space in a disjoint union is an isometry -/
theorem isometry_inr : Isometry (Sum.inr : Y ‚Üí X ‚äï Y) :=
  Isometry.of_dist_eq fun _ _ => rfl

end Sum

namespace Sigma

/- Copy of the previous paragraph, but for arbitrary disjoint unions instead of the disjoint union
of two spaces. I.e., work with sigma types instead of sum types. -/
variable {Œπ : Type*} {E : Œπ ‚Üí Type*} [‚àÄ i, MetricSpace (E i)]

open Classical in
/-- Distance on a disjoint union. There are many (noncanonical) ways to put a distance compatible
with each factor.
We choose a construction that works for unbounded spaces, but requires basepoints,
chosen arbitrarily.
We embed isometrically each factor, set the basepoints at distance 1, arbitrarily,
and say that the distance from `a` to `b` is the sum of the distances of `a` and `b` to
their respective basepoints, plus the distance 1 between the basepoints.
Since there is an arbitrary choice in this construction, it is not an instance by default. -/
protected def dist : (Œ£ i, E i) ‚Üí (Œ£ i, E i) ‚Üí ‚Ñù
  | ‚ü®i, x‚ü©, ‚ü®j, y‚ü© =>
    if h : i = j then
      haveI : E j = E i := by rw [h]
      Dist.dist x (cast this y)
    else Dist.dist x (Nonempty.some ‚ü®x‚ü©) + 1 + Dist.dist (Nonempty.some ‚ü®y‚ü©) y

/-- A `Dist` instance on the disjoint union `Œ£ i, E i`.
We embed isometrically each factor, set the basepoints at distance 1, arbitrarily,
and say that the distance from `a` to `b` is the sum of the distances of `a` and `b` to
their respective basepoints, plus the distance 1 between the basepoints.
Since there is an arbitrary choice in this construction, it is not an instance by default. -/
def instDist : Dist (Œ£ i, E i) :=
  ‚ü®Sigma.dist‚ü©

attribute [local instance] Sigma.instDist

@[simp]
theorem dist_same (i : Œπ) (x y : E i) : dist (Sigma.mk i x) ‚ü®i, y‚ü© = dist x y := by
  simp [Dist.dist, Sigma.dist]

@[simp]
theorem dist_ne {i j : Œπ} (h : i ‚â† j) (x : E i) (y : E j) :
    dist (‚ü®i, x‚ü© : Œ£ k, E k) ‚ü®j, y‚ü© = dist x (Nonempty.some ‚ü®x‚ü©) + 1 + dist (Nonempty.some ‚ü®y‚ü©) y :=
  dif_neg h

theorem one_le_dist_of_ne {i j : Œπ} (h : i ‚â† j) (x : E i) (y : E j) :
    1 ‚â§ dist (‚ü®i, x‚ü© : Œ£ k, E k) ‚ü®j, y‚ü© := by
  rw [Sigma.dist_ne h x y]
  linarith [@dist_nonneg _ _ x (Nonempty.some ‚ü®x‚ü©), @dist_nonneg _ _ (Nonempty.some ‚ü®y‚ü©) y]

theorem fst_eq_of_dist_lt_one (x y : Œ£ i, E i) (h : dist x y < 1) : x.1 = y.1 := by
  cases x; cases y
  contrapose! h
  apply one_le_dist_of_ne h

protected theorem dist_triangle (x y z : Œ£ i, E i) : dist x z ‚â§ dist x y + dist y z := by
  rcases x with ‚ü®i, x‚ü©; rcases y with ‚ü®j, y‚ü©; rcases z with ‚ü®k, z‚ü©
  rcases eq_or_ne i k with (rfl | hik)
  ¬∑ rcases eq_or_ne i j with (rfl | hij)
    ¬∑ simpa using dist_triangle x y z
    ¬∑ simp only [Sigma.dist_same, Sigma.dist_ne hij, Sigma.dist_ne hij.symm]
      calc
        dist x z ‚â§ dist x (Nonempty.some ‚ü®x‚ü©) + 0 + 0 + (0 + 0 + dist (Nonempty.some ‚ü®z‚ü©) z) := by
          simpa only [zero_add, add_zero] using dist_triangle _ _ _
        _ ‚â§ _ := by apply_rules [add_le_add, le_rfl, dist_nonneg, zero_le_one]
  ¬∑ rcases eq_or_ne i j with (rfl | hij)
    ¬∑ simp only [Sigma.dist_ne hik, Sigma.dist_same]
      calc
        dist x (Nonempty.some ‚ü®x‚ü©) + 1 + dist (Nonempty.some ‚ü®z‚ü©) z ‚â§
            dist x y + dist y (Nonempty.some ‚ü®y‚ü©) + 1 + dist (Nonempty.some ‚ü®z‚ü©) z := by
          apply_rules [add_le_add, le_rfl, dist_triangle]
        _ = _ := by abel
    ¬∑ rcases eq_or_ne j k with (rfl | hjk)
      ¬∑ simp only [Sigma.dist_ne hij, Sigma.dist_same]
        calc
          dist x (Nonempty.some ‚ü®x‚ü©) + 1 + dist (Nonempty.some ‚ü®z‚ü©) z ‚â§
              dist x (Nonempty.some ‚ü®x‚ü©) + 1 + (dist (Nonempty.some ‚ü®z‚ü©) y + dist y z) := by
            apply_rules [add_le_add, le_rfl, dist_triangle]
          _ = _ := by abel
      ¬∑ simp only [hik, hij, hjk, Sigma.dist_ne, Ne, not_false_iff]
        calc
          dist x (Nonempty.some ‚ü®x‚ü©) + 1 + dist (Nonempty.some ‚ü®z‚ü©) z =
              dist x (Nonempty.some ‚ü®x‚ü©) + 1 + 0 + (0 + 0 + dist (Nonempty.some ‚ü®z‚ü©) z) := by
            simp only [add_zero, zero_add]
          _ ‚â§ _ := by apply_rules [add_le_add, zero_le_one, dist_nonneg, le_rfl]

protected theorem isOpen_iff (s : Set (Œ£ i, E i)) :
    IsOpen s ‚Üî ‚àÄ x ‚àà s, ‚àÉ Œµ > 0, ‚àÄ y, dist x y < Œµ ‚Üí y ‚àà s := by
  constructor
  ¬∑ rintro hs ‚ü®i, x‚ü© hx
    obtain ‚ü®Œµ, Œµpos, hŒµ‚ü© : ‚àÉ Œµ > 0, ball x Œµ ‚äÜ Sigma.mk i ‚Åª¬π' s :=
      Metric.isOpen_iff.1 (isOpen_sigma_iff.1 hs i) x hx
    refine ‚ü®min Œµ 1, lt_min Œµpos zero_lt_one, ?_‚ü©
    rintro ‚ü®j, y‚ü© hy
    rcases eq_or_ne i j with (rfl | hij)
    ¬∑ simp only [Sigma.dist_same, lt_min_iff] at hy
      exact hŒµ (mem_ball'.2 hy.1)
    ¬∑ apply (lt_irrefl (1 : ‚Ñù) _).elim
      calc
        1 ‚â§ Sigma.dist ‚ü®i, x‚ü© ‚ü®j, y‚ü© := Sigma.one_le_dist_of_ne hij _ _
        _ < 1 := hy.trans_le (min_le_right _ _)
  ¬∑ refine fun H => isOpen_sigma_iff.2 fun i => Metric.isOpen_iff.2 fun x hx => ?_
    obtain ‚ü®Œµ, Œµpos, hŒµ‚ü© : ‚àÉ Œµ > 0, ‚àÄ y, dist (‚ü®i, x‚ü© : Œ£ j, E j) y < Œµ ‚Üí y ‚àà s :=
      H ‚ü®i, x‚ü© hx
    refine ‚ü®Œµ, Œµpos, fun y hy => ?_‚ü©
    apply hŒµ ‚ü®i, y‚ü©
    rw [Sigma.dist_same]
    exact mem_ball'.1 hy

/-- A metric space structure on the disjoint union `Œ£ i, E i`.
We embed isometrically each factor, set the basepoints at distance 1, arbitrarily,
and say that the distance from `a` to `b` is the sum of the distances of `a` and `b` to
their respective basepoints, plus the distance 1 between the basepoints.
Since there is an arbitrary choice in this construction, it is not an instance by default. -/
protected def metricSpace : MetricSpace (Œ£ i, E i) := by
  refine MetricSpace.ofDistTopology Sigma.dist ?_ ?_ Sigma.dist_triangle Sigma.isOpen_iff ?_
  ¬∑ rintro ‚ü®i, x‚ü©
    simp [Sigma.dist]
  ¬∑ rintro ‚ü®i, x‚ü© ‚ü®j, y‚ü©
    rcases eq_or_ne i j with (rfl | h)
    ¬∑ simp [Sigma.dist, dist_comm]
    ¬∑ simp only [Sigma.dist, dist_comm, h, h.symm, not_false_iff, dif_neg]
      abel
  ¬∑ rintro ‚ü®i, x‚ü© ‚ü®j, y‚ü©
    rcases eq_or_ne i j with (rfl | hij)
    ¬∑ simp [Sigma.dist]
    ¬∑ intro h
      apply (lt_irrefl (1 : ‚Ñù) _).elim
      calc
        1 ‚â§ Sigma.dist (‚ü®i, x‚ü© : Œ£ k, E k) ‚ü®j, y‚ü© := Sigma.one_le_dist_of_ne hij _ _
        _ < 1 := by rw [h]; exact zero_lt_one

attribute [local instance] Sigma.metricSpace

open Topology

open Filter

/-- The injection of a space in a disjoint union is an isometry -/
theorem isometry_mk (i : Œπ) : Isometry (Sigma.mk i : E i ‚Üí Œ£ k, E k) :=
  Isometry.of_dist_eq fun x y => by simp

/-- A disjoint union of complete metric spaces is complete. -/
protected theorem completeSpace [‚àÄ i, CompleteSpace (E i)] : CompleteSpace (Œ£ i, E i) := by
  set s : Œπ ‚Üí Set (Œ£ i, E i) := fun i => Sigma.fst ‚Åª¬π' {i}
  set U := { p : (Œ£ k, E k) √ó Œ£ k, E k | dist p.1 p.2 < 1 }
  have hc : ‚àÄ i, IsComplete (s i) := fun i => by
    simp only [s, ‚Üê range_sigmaMk]
    exact (isometry_mk i).isUniformInducing.isComplete_range
  have hd : ‚àÄ (i j), ‚àÄ x ‚àà s i, ‚àÄ y ‚àà s j, (x, y) ‚àà U ‚Üí i = j := fun i j x hx y hy hxy =>
    (Eq.symm hx).trans ((fst_eq_of_dist_lt_one _ _ hxy).trans hy)
  refine completeSpace_of_isComplete_univ ?_
  convert isComplete_iUnion_separated hc (dist_mem_uniformity zero_lt_one) hd
  simp only [s, ‚Üê preimage_iUnion, iUnion_of_singleton, preimage_univ]

end Sigma

section Gluing

-- Exact gluing of two metric spaces along isometric subsets.
variable {X : Type u} {Y : Type v} {Z : Type w}
variable [Nonempty Z] [MetricSpace Z] [MetricSpace X] [MetricSpace Y] {Œ¶ : Z ‚Üí X} {Œ® : Z ‚Üí Y}
  {Œµ : ‚Ñù}

/-- Given two isometric embeddings `Œ¶ : Z ‚Üí X` and `Œ® : Z ‚Üí Y`, we define a pseudo metric space
structure on `X ‚äï Y` by declaring that `Œ¶ x` and `Œ® x` are at distance `0`. -/
def gluePremetric (hŒ¶ : Isometry Œ¶) (hŒ® : Isometry Œ®) : PseudoMetricSpace (X ‚äï Y) where
  dist := glueDist Œ¶ Œ® 0
  dist_self := glueDist_self Œ¶ Œ® 0
  dist_comm := glueDist_comm Œ¶ Œ® 0
  dist_triangle := glueDist_triangle Œ¶ Œ® 0 fun p q => by rw [hŒ¶.dist_eq, hŒ®.dist_eq]; simp

/-- Given two isometric embeddings `Œ¶ : Z ‚Üí X` and `Œ® : Z ‚Üí Y`, we define a
space `GlueSpace hŒ¶ hŒ®` by identifying in `X ‚äï Y` the points `Œ¶ x` and `Œ® x`. -/
def GlueSpace (hŒ¶ : Isometry Œ¶) (hŒ® : Isometry Œ®) : Type _ :=
  @SeparationQuotient _ (gluePremetric hŒ¶ hŒ®).toUniformSpace.toTopologicalSpace

instance (hŒ¶ : Isometry Œ¶) (hŒ® : Isometry Œ®) : MetricSpace (GlueSpace hŒ¶ hŒ®) :=
  inferInstanceAs <| MetricSpace <|
    @SeparationQuotient _ (gluePremetric hŒ¶ hŒ®).toUniformSpace.toTopologicalSpace

/-- The canonical map from `X` to the space obtained by gluing isometric subsets in `X` and `Y`. -/
def toGlueL (hŒ¶ : Isometry Œ¶) (hŒ® : Isometry Œ®) (x : X) : GlueSpace hŒ¶ hŒ® :=
  Quotient.mk'' (.inl x)

/-- The canonical map from `Y` to the space obtained by gluing isometric subsets in `X` and `Y`. -/
def toGlueR (hŒ¶ : Isometry Œ¶) (hŒ® : Isometry Œ®) (y : Y) : GlueSpace hŒ¶ hŒ® :=
  Quotient.mk'' (.inr y)

instance inhabitedLeft (hŒ¶ : Isometry Œ¶) (hŒ® : Isometry Œ®) [Inhabited X] :
    Inhabited (GlueSpace hŒ¶ hŒ®) :=
  ‚ü®toGlueL _ _ default‚ü©

instance inhabitedRight (hŒ¶ : Isometry Œ¶) (hŒ® : Isometry Œ®) [Inhabited Y] :
    Inhabited (GlueSpace hŒ¶ hŒ®) :=
  ‚ü®toGlueR _ _ default‚ü©

theorem toGlue_commute (hŒ¶ : Isometry Œ¶) (hŒ® : Isometry Œ®) :
    toGlueL hŒ¶ hŒ® ‚àò Œ¶ = toGlueR hŒ¶ hŒ® ‚àò Œ® := by
  let i : PseudoMetricSpace (X ‚äï Y) := gluePremetric hŒ¶ hŒ®
  let _ := i.toUniformSpace.toTopologicalSpace
  funext
  simp only [comp, toGlueL, toGlueR]
  refine SeparationQuotient.mk_eq_mk.2 (Metric.inseparable_iff.2 ?_)
  exact glueDist_glued_points Œ¶ Œ® 0 _

theorem toGlueL_isometry (hŒ¶ : Isometry Œ¶) (hŒ® : Isometry Œ®) : Isometry (toGlueL hŒ¶ hŒ®) :=
  Isometry.of_dist_eq fun _ _ => rfl

theorem toGlueR_isometry (hŒ¶ : Isometry Œ¶) (hŒ® : Isometry Œ®) : Isometry (toGlueR hŒ¶ hŒ®) :=
  Isometry.of_dist_eq fun _ _ => rfl

end Gluing --section

section InductiveLimit

/-!
### Inductive limit of metric spaces

In this section, we define the inductive limit of

```
     f 0        f 1        f 2        f 3
X 0 -----> X 1 -----> X 2 -----> X 3 -----> ...
```

where the `X n` are metric spaces and f n isometric embeddings. We do it by defining a premetric
space structure on `Œ£ n, X n`, where the predistance `dist x y` is obtained by pushing `x` and `y`
in a common `X k` using composition by the `f n`, and taking the distance there. This does not
depend on the choice of `k` as the `f n` are isometries. The metric space associated to this
premetric space is the desired inductive limit.
-/

open Nat

variable {X : ‚Ñï ‚Üí Type u} [‚àÄ n, MetricSpace (X n)] {f : ‚àÄ n, X n ‚Üí X (n + 1)}

/-- Predistance on the disjoint union `Œ£ n, X n`. -/
def inductiveLimitDist (f : ‚àÄ n, X n ‚Üí X (n + 1)) (x y : Œ£ n, X n) : ‚Ñù :=
  dist (leRecOn (le_max_left x.1 y.1) (f _) x.2 : X (max x.1 y.1))
    (leRecOn (le_max_right x.1 y.1) (f _) y.2 : X (max x.1 y.1))

/-- The predistance on the disjoint union `Œ£ n, X n` can be computed in any `X k` for large
enough `k`. -/
theorem inductiveLimitDist_eq_dist (I : ‚àÄ n, Isometry (f n)) (x y : Œ£ n, X n) :
    ‚àÄ m (hx : x.1 ‚â§ m) (hy : y.1 ‚â§ m), inductiveLimitDist f x y =
      dist (leRecOn hx (f _) x.2 : X m) (leRecOn hy (f _) y.2 : X m)
  | 0, hx, hy => by
    obtain ‚ü®i, x‚ü© := x; obtain ‚ü®j, y‚ü© := y
    obtain rfl : i = 0 := nonpos_iff_eq_zero.1 hx
    obtain rfl : j = 0 := nonpos_iff_eq_zero.1 hy
    rfl
  | (m + 1), hx, hy => by
    by_cases h : max x.1 y.1 = (m + 1)
    ¬∑ generalize m + 1 = m' at *
      subst m'
      rfl
    ¬∑ have : max x.1 y.1 ‚â§ succ m := by simp [hx, hy]
      have : max x.1 y.1 ‚â§ m := by simpa [h] using of_le_succ this
      have xm : x.1 ‚â§ m := le_trans (le_max_left _ _) this
      have ym : y.1 ‚â§ m := le_trans (le_max_right _ _) this
      rw [leRecOn_succ xm, leRecOn_succ ym, (I m).dist_eq]
      exact inductiveLimitDist_eq_dist I x y m xm ym

/-- Premetric space structure on `Œ£ n, X n`. -/
def inductivePremetric (I : ‚àÄ n, Isometry (f n)) : PseudoMetricSpace (Œ£ n, X n) where
  dist := inductiveLimitDist f
  dist_self x := by simp [inductiveLimitDist]
  dist_comm x y := by
    let m := max x.1 y.1
    have hx : x.1 ‚â§ m := le_max_left _ _
    have hy : y.1 ‚â§ m := le_max_right _ _
    rw [inductiveLimitDist_eq_dist I x y m hx hy, inductiveLimitDist_eq_dist I y x m hy hx,
      dist_comm]
  dist_triangle x y z := by
    let m := max (max x.1 y.1) z.1
    have hx : x.1 ‚â§ m := le_trans (le_max_left _ _) (le_max_left _ _)
    have hy : y.1 ‚â§ m := le_trans (le_max_right _ _) (le_max_left _ _)
    have hz : z.1 ‚â§ m := le_max_right _ _
    calc
      inductiveLimitDist f x z = dist (leRecOn hx (f _) x.2 : X m) (leRecOn hz (f _) z.2 : X m) :=
        inductiveLimitDist_eq_dist I x z m hx hz
      _ ‚â§ dist (leRecOn hx (f _) x.2 : X m) (leRecOn hy (f _) y.2 : X m) +
            dist (leRecOn hy (f _) y.2 : X m) (leRecOn hz (f _) z.2 : X m) :=
        (dist_triangle _ _ _)
      _ = inductiveLimitDist f x y + inductiveLimitDist f y z := by
        rw [inductiveLimitDist_eq_dist I x y m hx hy, inductiveLimitDist_eq_dist I y z m hy hz]

attribute [local instance] inductivePremetric

/-- The type giving the inductive limit in a metric space context. -/
def InductiveLimit (I : ‚àÄ n, Isometry (f n)) : Type _ :=
  @SeparationQuotient _ (inductivePremetric I).toUniformSpace.toTopologicalSpace

instance {I : ‚àÄ (n : ‚Ñï), Isometry (f n)} : MetricSpace (InductiveLimit (f := f) I) :=
  inferInstanceAs <| MetricSpace <|
    @SeparationQuotient _ (inductivePremetric I).toUniformSpace.toTopologicalSpace

/-- Mapping each `X n` to the inductive limit. -/
def toInductiveLimit (I : ‚àÄ n, Isometry (f n)) (n : ‚Ñï) (x : X n) : Metric.InductiveLimit I :=
  Quotient.mk'' (Sigma.mk n x)

instance (I : ‚àÄ n, Isometry (f n)) [Inhabited (X 0)] : Inhabited (InductiveLimit I) :=
  ‚ü®toInductiveLimit _ 0 default‚ü©

/-- The map `toInductiveLimit n` mapping `X n` to the inductive limit is an isometry. -/
theorem toInductiveLimit_isometry (I : ‚àÄ n, Isometry (f n)) (n : ‚Ñï) :
    Isometry (toInductiveLimit I n) :=
  Isometry.of_dist_eq fun x y => by
    change inductiveLimitDist f ‚ü®n, x‚ü© ‚ü®n, y‚ü© = dist x y
    rw [inductiveLimitDist_eq_dist I ‚ü®n, x‚ü© ‚ü®n, y‚ü© n (le_refl n) (le_refl n), leRecOn_self,
      leRecOn_self]

/-- The maps `toInductiveLimit n` are compatible with the maps `f n`. -/
theorem toInductiveLimit_commute (I : ‚àÄ n, Isometry (f n)) (n : ‚Ñï) :
    toInductiveLimit I n.succ ‚àò f n = toInductiveLimit I n := by
  let h := inductivePremetric I
  let _ := h.toUniformSpace.toTopologicalSpace
  funext x
  simp only [comp, toInductiveLimit]
  refine SeparationQuotient.mk_eq_mk.2 (Metric.inseparable_iff.2 ?_)
  change inductiveLimitDist f ‚ü®n.succ, f n x‚ü© ‚ü®n, x‚ü© = 0
  rw [inductiveLimitDist_eq_dist I ‚ü®n.succ, f n x‚ü© ‚ü®n, x‚ü© n.succ, leRecOn_self,
    leRecOn_succ, leRecOn_self, dist_self]
  ¬∑ rfl
  ¬∑ rfl
  ¬∑ exact le_succ _

theorem dense_iUnion_range_toInductiveLimit
    {X : ‚Ñï ‚Üí Type u} [(n : ‚Ñï) ‚Üí MetricSpace (X n)]
    {f : (n : ‚Ñï) ‚Üí X n ‚Üí X (n + 1)}
    (I : ‚àÄ (n : ‚Ñï), Isometry (f n)) :
    Dense (‚ãÉ i, range (toInductiveLimit I i)) := by
  refine dense_univ.mono ?_
  rintro ‚ü®n, x‚ü© _
  refine mem_iUnion.2 ‚ü®n, mem_range.2 ‚ü®x, rfl‚ü©‚ü©

theorem separableSpaceInductiveLimit_of_separableSpace
    {X : ‚Ñï ‚Üí Type u} [(n : ‚Ñï) ‚Üí MetricSpace (X n)]
    [hs : (n : ‚Ñï) ‚Üí TopologicalSpace.SeparableSpace (X n)] {f : (n : ‚Ñï) ‚Üí X n ‚Üí X (n + 1)}
    (I : ‚àÄ (n : ‚Ñï), Isometry (f n)) :
    TopologicalSpace.SeparableSpace (Metric.InductiveLimit I) := by
  choose hsX hcX hdX using (fun n ‚Ü¶ TopologicalSpace.exists_countable_dense (X n))
  let s := ‚ãÉ (i : ‚Ñï), (toInductiveLimit I i '' (hsX i))
  refine ‚ü®s, countable_iUnion (fun n => (hcX n).image _), ?_‚ü©
  refine .of_closure <| (dense_iUnion_range_toInductiveLimit I).mono <| iUnion_subset fun i ‚Ü¶ ?_
  calc
    range (toInductiveLimit I i) ‚äÜ closure (toInductiveLimit I i '' (hsX i)) :=
      (toInductiveLimit_isometry I i |>.continuous).range_subset_closure_image_dense (hdX i)
    _ ‚äÜ closure s := closure_mono <| subset_iUnion (fun j ‚Ü¶ toInductiveLimit I j '' hsX j) i

end InductiveLimit --section

end Metric --namespace
