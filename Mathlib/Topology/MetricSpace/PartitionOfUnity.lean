/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Topology.EMetricSpace.Paracompact
import Mathlib.Topology.Instances.ENNReal
import Mathlib.Analysis.Convex.PartitionOfUnity

#align_import topology.metric_space.partition_of_unity from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Lemmas about (e)metric spaces that need partition of unity

The main lemma in this file (see `Metric.exists_continuous_real_forall_closedBall_subset`) says the
following. Let `X` be a metric space. Let `K : Î¹ â†’ Set X` be a locally finite family of closed sets,
let `U : Î¹ â†’ Set X` be a family of open sets such that `K i âŠ† U i` for all `i`. Then there exists a
positive continuous function `Î´ : C(X, â†’ â„)` such that for any `i` and `x âˆˆ K i`, we have
`Metric.closedBall x (Î´ x) âŠ† U i`. We also formulate versions of this lemma for extended metric
spaces and for different codomains (`â„`, `â„â‰¥0`, and `â„â‰¥0âˆ`).

We also prove a few auxiliary lemmas to be used later in a proof of the smooth version of this
lemma.

## Tags

metric space, partition of unity, locally finite
-/

open Topology ENNReal NNReal Filter Set Function TopologicalSpace

variable {Î¹ X : Type*}

namespace EMetric

variable [EMetricSpace X] {K : Î¹ â†’ Set X} {U : Î¹ â†’ Set X}

/-- Let `K : Î¹ â†’ Set X` be a locally finite family of closed sets in an emetric space. Let
`U : Î¹ â†’ Set X` be a family of open sets such that `K i âŠ† U i` for all `i`. Then for any point
`x : X`, for sufficiently small `r : â„â‰¥0âˆ` and for `y` sufficiently close to `x`, for all `i`, if
`y âˆˆ K i`, then `EMetric.closedBall y r âŠ† U i`. -/
theorem eventually_nhds_zero_forall_closedBall_subset (hK : âˆ€ i, IsClosed (K i))
    (hU : âˆ€ i, IsOpen (U i)) (hKU : âˆ€ i, K i âŠ† U i) (hfin : LocallyFinite K) (x : X) :
    âˆ€á¶  p : â„â‰¥0âˆ Ã— X in ğ“ 0 Ã—Ë¢ ğ“ x, âˆ€ i, p.2 âˆˆ K i â†’ closedBall p.2 p.1 âŠ† U i := by
  suffices âˆ€ i, x âˆˆ K i â†’ âˆ€á¶  p : â„â‰¥0âˆ Ã— X in ğ“ 0 Ã—Ë¢ ğ“ x, closedBall p.2 p.1 âŠ† U i by
    apply mp_mem ((eventually_all_finite (hfin.point_finite x)).2 this)
      (mp_mem (@tendsto_snd â„â‰¥0âˆ _ (ğ“ 0) _ _ (hfin.iInter_compl_mem_nhds hK x)) _)
    apply univ_mem'
    rintro âŸ¨r, yâŸ© hxy hyU i hi
    simp only [mem_iInter, mem_compl_iff, not_imp_not, mem_preimage] at hxy
    exact hyU _ (hxy _ hi)
  intro i hi
  rcases nhds_basis_closed_eball.mem_iff.1 ((hU i).mem_nhds <| hKU i hi) with âŸ¨R, hRâ‚€, hRâŸ©
  rcases ENNReal.lt_iff_exists_nnreal_btwn.mp hRâ‚€ with âŸ¨r, hrâ‚€, hrRâŸ©
  filter_upwards [prod_mem_prod (eventually_lt_nhds hrâ‚€)
      (closedBall_mem_nhds x (tsub_pos_iff_lt.2 hrR))] with p hp z hz
  apply hR
  calc
    edist z x â‰¤ edist z p.2 + edist p.2 x := edist_triangle _ _ _
    _ â‰¤ p.1 + (R - p.1) := add_le_add hz <| le_trans hp.2 <| tsub_le_tsub_left hp.1.out.le _
    _ = R := add_tsub_cancel_of_le (lt_trans (by exact hp.1) hrR).le
#align emetric.eventually_nhds_zero_forall_closed_ball_subset EMetric.eventually_nhds_zero_forall_closedBall_subset

theorem exists_forall_closedBall_subset_auxâ‚ (hK : âˆ€ i, IsClosed (K i)) (hU : âˆ€ i, IsOpen (U i))
    (hKU : âˆ€ i, K i âŠ† U i) (hfin : LocallyFinite K) (x : X) :
    âˆƒ r : â„, âˆ€á¶  y in ğ“ x,
      r âˆˆ Ioi (0 : â„) âˆ© ENNReal.ofReal â»Â¹' â‹‚ (i) (_ : y âˆˆ K i), { r | closedBall y r âŠ† U i } := by
  have := (ENNReal.continuous_ofReal.tendsto' 0 0 ENNReal.ofReal_zero).eventually
    (eventually_nhds_zero_forall_closedBall_subset hK hU hKU hfin x).curry
  rcases this.exists_gt with âŸ¨r, hr0, hrâŸ©
  refine âŸ¨r, hr.mono fun y hy => âŸ¨hr0, ?_âŸ©âŸ©
  rwa [mem_preimage, mem_iInterâ‚‚]
#align emetric.exists_forall_closed_ball_subset_auxâ‚ EMetric.exists_forall_closedBall_subset_auxâ‚

theorem exists_forall_closedBall_subset_auxâ‚‚ (y : X) :
    Convex â„
      (Ioi (0 : â„) âˆ© ENNReal.ofReal â»Â¹' â‹‚ (i) (_ : y âˆˆ K i), { r | closedBall y r âŠ† U i }) :=
  (convex_Ioi _).inter <| OrdConnected.convex <| OrdConnected.preimage_ennreal_ofReal <|
    ordConnected_iInter fun i => ordConnected_iInter fun (_ : y âˆˆ K i) =>
      ordConnected_setOf_closedBall_subset y (U i)
#align emetric.exists_forall_closed_ball_subset_auxâ‚‚ EMetric.exists_forall_closedBall_subset_auxâ‚‚

/-- Let `X` be an extended metric space. Let `K : Î¹ â†’ Set X` be a locally finite family of closed
sets, let `U : Î¹ â†’ Set X` be a family of open sets such that `K i âŠ† U i` for all `i`. Then there
exists a positive continuous function `Î´ : C(X, â„)` such that for any `i` and `x âˆˆ K i`,
we have `EMetric.closedBall x (ENNReal.ofReal (Î´ x)) âŠ† U i`. -/
theorem exists_continuous_real_forall_closedBall_subset (hK : âˆ€ i, IsClosed (K i))
    (hU : âˆ€ i, IsOpen (U i)) (hKU : âˆ€ i, K i âŠ† U i) (hfin : LocallyFinite K) :
    âˆƒ Î´ : C(X, â„), (âˆ€ x, 0 < Î´ x) âˆ§
      âˆ€ (i), âˆ€ x âˆˆ K i, closedBall x (ENNReal.ofReal <| Î´ x) âŠ† U i := by
  simpa only [mem_inter_iff, forall_and, mem_preimage, mem_iInter, @forall_swap Î¹ X] using
    exists_continuous_forall_mem_convex_of_local_const exists_forall_closedBall_subset_auxâ‚‚
      (exists_forall_closedBall_subset_auxâ‚ hK hU hKU hfin)
#align emetric.exists_continuous_real_forall_closed_ball_subset EMetric.exists_continuous_real_forall_closedBall_subset

/-- Let `X` be an extended metric space. Let `K : Î¹ â†’ Set X` be a locally finite family of closed
sets, let `U : Î¹ â†’ Set X` be a family of open sets such that `K i âŠ† U i` for all `i`. Then there
exists a positive continuous function `Î´ : C(X, â„â‰¥0)` such that for any `i` and `x âˆˆ K i`,
we have `EMetric.closedBall x (Î´ x) âŠ† U i`. -/
theorem exists_continuous_nnreal_forall_closedBall_subset (hK : âˆ€ i, IsClosed (K i))
    (hU : âˆ€ i, IsOpen (U i)) (hKU : âˆ€ i, K i âŠ† U i) (hfin : LocallyFinite K) :
    âˆƒ Î´ : C(X, â„â‰¥0), (âˆ€ x, 0 < Î´ x) âˆ§ âˆ€ (i), âˆ€ x âˆˆ K i, closedBall x (Î´ x) âŠ† U i := by
  rcases exists_continuous_real_forall_closedBall_subset hK hU hKU hfin with âŸ¨Î´, hÎ´â‚€, hÎ´âŸ©
  lift Î´ to C(X, â„â‰¥0) using fun x => (hÎ´â‚€ x).le
  refine âŸ¨Î´, hÎ´â‚€, fun i x hi => ?_âŸ©
  simpa only [â† ENNReal.ofReal_coe_nnreal] using hÎ´ i x hi
#align emetric.exists_continuous_nnreal_forall_closed_ball_subset EMetric.exists_continuous_nnreal_forall_closedBall_subset

/-- Let `X` be an extended metric space. Let `K : Î¹ â†’ Set X` be a locally finite family of closed
sets, let `U : Î¹ â†’ Set X` be a family of open sets such that `K i âŠ† U i` for all `i`. Then there
exists a positive continuous function `Î´ : C(X, â„â‰¥0âˆ)` such that for any `i` and `x âˆˆ K i`,
we have `EMetric.closedBall x (Î´ x) âŠ† U i`. -/
theorem exists_continuous_eNNReal_forall_closedBall_subset (hK : âˆ€ i, IsClosed (K i))
    (hU : âˆ€ i, IsOpen (U i)) (hKU : âˆ€ i, K i âŠ† U i) (hfin : LocallyFinite K) :
    âˆƒ Î´ : C(X, â„â‰¥0âˆ), (âˆ€ x, 0 < Î´ x) âˆ§ âˆ€ (i), âˆ€ x âˆˆ K i, closedBall x (Î´ x) âŠ† U i :=
  let âŸ¨Î´, hÎ´â‚€, hÎ´âŸ© := exists_continuous_nnreal_forall_closedBall_subset hK hU hKU hfin
  âŸ¨ContinuousMap.comp âŸ¨Coe.coe, ENNReal.continuous_coeâŸ© Î´, fun x => ENNReal.coe_pos.2 (hÎ´â‚€ x), hÎ´âŸ©
#align emetric.exists_continuous_ennreal_forall_closed_ball_subset EMetric.exists_continuous_eNNReal_forall_closedBall_subset

end EMetric

namespace Metric

variable [MetricSpace X] {K : Î¹ â†’ Set X} {U : Î¹ â†’ Set X}

/-- Let `X` be a metric space. Let `K : Î¹ â†’ Set X` be a locally finite family of closed sets, let
`U : Î¹ â†’ Set X` be a family of open sets such that `K i âŠ† U i` for all `i`. Then there exists a
positive continuous function `Î´ : C(X, â„â‰¥0)` such that for any `i` and `x âˆˆ K i`, we have
`Metric.closedBall x (Î´ x) âŠ† U i`. -/
theorem exists_continuous_nnreal_forall_closedBall_subset (hK : âˆ€ i, IsClosed (K i))
    (hU : âˆ€ i, IsOpen (U i)) (hKU : âˆ€ i, K i âŠ† U i) (hfin : LocallyFinite K) :
    âˆƒ Î´ : C(X, â„â‰¥0), (âˆ€ x, 0 < Î´ x) âˆ§ âˆ€ (i), âˆ€ x âˆˆ K i, closedBall x (Î´ x) âŠ† U i := by
  rcases EMetric.exists_continuous_nnreal_forall_closedBall_subset hK hU hKU hfin with âŸ¨Î´, hÎ´0, hÎ´âŸ©
  refine âŸ¨Î´, hÎ´0, fun i x hx => ?_âŸ©
  rw [â† emetric_closedBall_nnreal]
  exact hÎ´ i x hx
#align metric.exists_continuous_nnreal_forall_closed_ball_subset Metric.exists_continuous_nnreal_forall_closedBall_subset

/-- Let `X` be a metric space. Let `K : Î¹ â†’ Set X` be a locally finite family of closed sets, let
`U : Î¹ â†’ Set X` be a family of open sets such that `K i âŠ† U i` for all `i`. Then there exists a
positive continuous function `Î´ : C(X, â„)` such that for any `i` and `x âˆˆ K i`, we have
`Metric.closedBall x (Î´ x) âŠ† U i`. -/
theorem exists_continuous_real_forall_closedBall_subset (hK : âˆ€ i, IsClosed (K i))
    (hU : âˆ€ i, IsOpen (U i)) (hKU : âˆ€ i, K i âŠ† U i) (hfin : LocallyFinite K) :
    âˆƒ Î´ : C(X, â„), (âˆ€ x, 0 < Î´ x) âˆ§ âˆ€ (i), âˆ€ x âˆˆ K i, closedBall x (Î´ x) âŠ† U i :=
  let âŸ¨Î´, hÎ´â‚€, hÎ´âŸ© := exists_continuous_nnreal_forall_closedBall_subset hK hU hKU hfin
  âŸ¨ContinuousMap.comp âŸ¨Coe.coe, NNReal.continuous_coeâŸ© Î´, hÎ´â‚€, hÎ´âŸ©
#align metric.exists_continuous_real_forall_closed_ball_subset Metric.exists_continuous_real_forall_closedBall_subset

end Metric
