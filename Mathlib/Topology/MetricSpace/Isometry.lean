/-
Copyright (c) 2018 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Data.Fintype.Lattice
import Mathlib.Data.Fintype.Sum
import Mathlib.Topology.Homeomorph.Lemmas
import Mathlib.Topology.MetricSpace.Antilipschitz

/-!
# Isometries

We define isometries, i.e., maps between emetric spaces that preserve
the edistance (on metric spaces, these are exactly the maps that preserve distances),
and prove their basic properties. We also introduce isometric bijections.

Since a lot of elementary properties don't require `eq_of_dist_eq_zero` we start setting up the
theory for `PseudoMetricSpace` and we specialize to `MetricSpace` when needed.
-/

open Topology

noncomputable section

universe u v w

variable {F Œπ : Type*} {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w}

open Function Set

open scoped Topology ENNReal

/-- An isometry (also known as isometric embedding) is a map preserving the edistance
between pseudoemetric spaces, or equivalently the distance between pseudometric space. -/
def Isometry [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] (f : Œ± ‚Üí Œ≤) : Prop :=
  ‚àÄ x1 x2 : Œ±, edist (f x1) (f x2) = edist x1 x2

/-- On pseudometric spaces, a map is an isometry if and only if it preserves nonnegative
distances. -/
theorem isometry_iff_nndist_eq [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} :
    Isometry f ‚Üî ‚àÄ x y, nndist (f x) (f y) = nndist x y := by
  simp only [Isometry, edist_nndist, ENNReal.coe_inj]

/-- On pseudometric spaces, a map is an isometry if and only if it preserves distances. -/
theorem isometry_iff_dist_eq [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} :
    Isometry f ‚Üî ‚àÄ x y, dist (f x) (f y) = dist x y := by
  simp only [isometry_iff_nndist_eq, ‚Üê coe_nndist, NNReal.coe_inj]

/-- An isometry preserves distances. -/
alias ‚ü®Isometry.dist_eq, _‚ü© := isometry_iff_dist_eq

/-- A map that preserves distances is an isometry -/
alias ‚ü®_, Isometry.of_dist_eq‚ü© := isometry_iff_dist_eq

/-- An isometry preserves non-negative distances. -/
alias ‚ü®Isometry.nndist_eq, _‚ü© := isometry_iff_nndist_eq

/-- A map that preserves non-negative distances is an isometry. -/
alias ‚ü®_, Isometry.of_nndist_eq‚ü© := isometry_iff_nndist_eq

namespace Isometry

section PseudoEmetricIsometry

variable [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] [PseudoEMetricSpace Œ≥]
variable {f : Œ± ‚Üí Œ≤} {x : Œ±}

/-- An isometry preserves edistances. -/
theorem edist_eq (hf : Isometry f) (x y : Œ±) : edist (f x) (f y) = edist x y :=
  hf x y

theorem lipschitz (h : Isometry f) : LipschitzWith 1 f :=
  LipschitzWith.of_edist_le fun x y => (h x y).le

theorem antilipschitz (h : Isometry f) : AntilipschitzWith 1 f := fun x y => by
  simp only [h x y, ENNReal.coe_one, one_mul, le_refl]

/-- Any map on a subsingleton is an isometry -/
@[nontriviality]
theorem _root_.isometry_subsingleton [Subsingleton Œ±] : Isometry f := fun x y => by
  rw [Subsingleton.elim x y]; simp

/-- The identity is an isometry -/
theorem _root_.isometry_id : Isometry (id : Œ± ‚Üí Œ±) := fun _ _ => rfl

theorem prodMap {Œ¥} [PseudoEMetricSpace Œ¥] {f : Œ± ‚Üí Œ≤} {g : Œ≥ ‚Üí Œ¥} (hf : Isometry f)
    (hg : Isometry g) : Isometry (Prod.map f g) := fun x y => by
  simp only [Prod.edist_eq, Prod.map_fst, hf.edist_eq, Prod.map_snd, hg.edist_eq]

@[deprecated (since := "2025-04-18")]
alias prod_map := prodMap

protected theorem piMap {Œπ} [Fintype Œπ] {Œ± Œ≤ : Œπ ‚Üí Type*} [‚àÄ i, PseudoEMetricSpace (Œ± i)]
    [‚àÄ i, PseudoEMetricSpace (Œ≤ i)] (f : ‚àÄ i, Œ± i ‚Üí Œ≤ i) (hf : ‚àÄ i, Isometry (f i)) :
    Isometry (Pi.map f) := fun x y => by
  simp only [edist_pi_def, (hf _).edist_eq, Pi.map_apply]

protected lemma single [Fintype Œπ] [DecidableEq Œπ] {E : Œπ ‚Üí Type*} [‚àÄ i, PseudoEMetricSpace (E i)]
    [‚àÄ i, Zero (E i)] (i : Œπ) :
    Isometry (Pi.single (M := E) i) := by
  intro x y
  rw [edist_pi_def]
  refine le_antisymm (Finset.sup_le fun j ‚Ü¶ ?_) (Finset.le_sup_of_le (Finset.mem_univ i) (by simp))
  obtain rfl | h := eq_or_ne i j
  ¬∑ simp
  ¬∑ simp [h]

protected lemma inl [AddZeroClass Œ±] [AddZeroClass Œ≤] : Isometry (AddMonoidHom.inl Œ± Œ≤) := by
  intro x y
  rw [Prod.edist_eq]
  simp

protected lemma inr [AddZeroClass Œ±] [AddZeroClass Œ≤] : Isometry (AddMonoidHom.inr Œ± Œ≤) := by
  intro x y
  rw [Prod.edist_eq]
  simp

/-- The composition of isometries is an isometry. -/
theorem comp {g : Œ≤ ‚Üí Œ≥} {f : Œ± ‚Üí Œ≤} (hg : Isometry g) (hf : Isometry f) : Isometry (g ‚àò f) :=
  fun _ _ => (hg _ _).trans (hf _ _)

/-- An isometry from a metric space is a uniform continuous map -/
protected theorem uniformContinuous (hf : Isometry f) : UniformContinuous f :=
  hf.lipschitz.uniformContinuous

/-- An isometry from a metric space is a uniform inducing map -/
theorem isUniformInducing (hf : Isometry f) : IsUniformInducing f :=
  hf.antilipschitz.isUniformInducing hf.uniformContinuous

theorem tendsto_nhds_iff {Œπ : Type*} {f : Œ± ‚Üí Œ≤} {g : Œπ ‚Üí Œ±} {a : Filter Œπ} {b : Œ±}
    (hf : Isometry f) : Filter.Tendsto g a (ùìù b) ‚Üî Filter.Tendsto (f ‚àò g) a (ùìù (f b)) :=
  hf.isUniformInducing.isInducing.tendsto_nhds_iff

/-- An isometry is continuous. -/
protected theorem continuous (hf : Isometry f) : Continuous f :=
  hf.lipschitz.continuous

/-- The right inverse of an isometry is an isometry. -/
theorem right_inv {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ±} (h : Isometry f) (hg : RightInverse g f) : Isometry g :=
  fun x y => by rw [‚Üê h, hg _, hg _]

theorem preimage_emetric_closedBall (h : Isometry f) (x : Œ±) (r : ‚Ñù‚â•0‚àû) :
    f ‚Åª¬π' EMetric.closedBall (f x) r = EMetric.closedBall x r := by
  ext y
  simp [h.edist_eq]

theorem preimage_emetric_ball (h : Isometry f) (x : Œ±) (r : ‚Ñù‚â•0‚àû) :
    f ‚Åª¬π' EMetric.ball (f x) r = EMetric.ball x r := by
  ext y
  simp [h.edist_eq]

/-- Isometries preserve the diameter in pseudoemetric spaces. -/
theorem ediam_image (hf : Isometry f) (s : Set Œ±) : EMetric.diam (f '' s) = EMetric.diam s :=
  eq_of_forall_ge_iff fun d => by simp only [EMetric.diam_le_iff, forall_mem_image, hf.edist_eq]

theorem ediam_range (hf : Isometry f) : EMetric.diam (range f) = EMetric.diam (univ : Set Œ±) := by
  rw [‚Üê image_univ]
  exact hf.ediam_image univ

theorem mapsTo_emetric_ball (hf : Isometry f) (x : Œ±) (r : ‚Ñù‚â•0‚àû) :
    MapsTo f (EMetric.ball x r) (EMetric.ball (f x) r) :=
  (hf.preimage_emetric_ball x r).ge

theorem mapsTo_emetric_closedBall (hf : Isometry f) (x : Œ±) (r : ‚Ñù‚â•0‚àû) :
    MapsTo f (EMetric.closedBall x r) (EMetric.closedBall (f x) r) :=
  (hf.preimage_emetric_closedBall x r).ge

/-- The injection from a subtype is an isometry -/
theorem _root_.isometry_subtype_coe {s : Set Œ±} : Isometry ((‚Üë) : s ‚Üí Œ±) := fun _ _ => rfl

theorem comp_continuousOn_iff {Œ≥} [TopologicalSpace Œ≥] (hf : Isometry f) {g : Œ≥ ‚Üí Œ±} {s : Set Œ≥} :
    ContinuousOn (f ‚àò g) s ‚Üî ContinuousOn g s :=
  hf.isUniformInducing.isInducing.continuousOn_iff.symm

theorem comp_continuous_iff {Œ≥} [TopologicalSpace Œ≥] (hf : Isometry f) {g : Œ≥ ‚Üí Œ±} :
    Continuous (f ‚àò g) ‚Üî Continuous g :=
  hf.isUniformInducing.isInducing.continuous_iff.symm

end PseudoEmetricIsometry

--section
section EmetricIsometry

variable [EMetricSpace Œ±] [PseudoEMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤}

/-- An isometry from an emetric space is injective -/
protected theorem injective (h : Isometry f) : Injective f :=
  h.antilipschitz.injective

/-- An isometry from an emetric space is a uniform embedding -/
lemma isUniformEmbedding (hf : Isometry f) : IsUniformEmbedding f :=
  hf.antilipschitz.isUniformEmbedding hf.lipschitz.uniformContinuous

/-- An isometry from an emetric space is an embedding -/
theorem isEmbedding (hf : Isometry f) : IsEmbedding f := hf.isUniformEmbedding.isEmbedding

/-- An isometry from a complete emetric space is a closed embedding -/
theorem isClosedEmbedding [CompleteSpace Œ±] [EMetricSpace Œ≥] {f : Œ± ‚Üí Œ≥} (hf : Isometry f) :
    IsClosedEmbedding f :=
  hf.antilipschitz.isClosedEmbedding hf.lipschitz.uniformContinuous

end EmetricIsometry

--section
section PseudoMetricIsometry

variable [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤}

/-- An isometry preserves the diameter in pseudometric spaces. -/
theorem diam_image (hf : Isometry f) (s : Set Œ±) : Metric.diam (f '' s) = Metric.diam s := by
  rw [Metric.diam, Metric.diam, hf.ediam_image]

theorem diam_range (hf : Isometry f) : Metric.diam (range f) = Metric.diam (univ : Set Œ±) := by
  rw [‚Üê image_univ]
  exact hf.diam_image univ

theorem preimage_setOf_dist (hf : Isometry f) (x : Œ±) (p : ‚Ñù ‚Üí Prop) :
    f ‚Åª¬π' { y | p (dist y (f x)) } = { y | p (dist y x) } := by
  simp [hf.dist_eq]

theorem preimage_closedBall (hf : Isometry f) (x : Œ±) (r : ‚Ñù) :
    f ‚Åª¬π' Metric.closedBall (f x) r = Metric.closedBall x r :=
  hf.preimage_setOf_dist x (¬∑ ‚â§ r)

theorem preimage_ball (hf : Isometry f) (x : Œ±) (r : ‚Ñù) :
    f ‚Åª¬π' Metric.ball (f x) r = Metric.ball x r :=
  hf.preimage_setOf_dist x (¬∑ < r)

theorem preimage_sphere (hf : Isometry f) (x : Œ±) (r : ‚Ñù) :
    f ‚Åª¬π' Metric.sphere (f x) r = Metric.sphere x r :=
  hf.preimage_setOf_dist x (¬∑ = r)

theorem mapsTo_ball (hf : Isometry f) (x : Œ±) (r : ‚Ñù) :
    MapsTo f (Metric.ball x r) (Metric.ball (f x) r) :=
  (hf.preimage_ball x r).ge

theorem mapsTo_sphere (hf : Isometry f) (x : Œ±) (r : ‚Ñù) :
    MapsTo f (Metric.sphere x r) (Metric.sphere (f x) r) :=
  (hf.preimage_sphere x r).ge

theorem mapsTo_closedBall (hf : Isometry f) (x : Œ±) (r : ‚Ñù) :
    MapsTo f (Metric.closedBall x r) (Metric.closedBall (f x) r) :=
  (hf.preimage_closedBall x r).ge

end PseudoMetricIsometry

-- section
end Isometry

-- namespace
/-- A uniform embedding from a uniform space to a metric space is an isometry with respect to the
induced metric space structure on the source space. -/
theorem IsUniformEmbedding.to_isometry {Œ± Œ≤} [UniformSpace Œ±] [MetricSpace Œ≤] {f : Œ± ‚Üí Œ≤}
    (h : IsUniformEmbedding f) : (letI := h.comapMetricSpace f; Isometry f) :=
  let _ := h.comapMetricSpace f
  Isometry.of_dist_eq fun _ _ => rfl

/-- An embedding from a topological space to a metric space is an isometry with respect to the
induced metric space structure on the source space. -/
theorem Topology.IsEmbedding.to_isometry {Œ± Œ≤} [TopologicalSpace Œ±] [MetricSpace Œ≤] {f : Œ± ‚Üí Œ≤}
    (h : IsEmbedding f) : (letI := h.comapMetricSpace f; Isometry f) :=
  let _ := h.comapMetricSpace f
  Isometry.of_dist_eq fun _ _ => rfl

theorem PseudoEMetricSpace.isometry_induced (f : Œ± ‚Üí Œ≤) [m : PseudoEMetricSpace Œ≤] :
    letI := m.induced f; Isometry f := fun _ _ ‚Ü¶ rfl

theorem PseudoMetricSpace.isometry_induced (f : Œ± ‚Üí Œ≤) [m : PseudoMetricSpace Œ≤] :
    letI := m.induced f; Isometry f := fun _ _ ‚Ü¶ rfl

@[deprecated (since := "2025-07-27")]
alias PsuedoMetricSpace.isometry_induced := PseudoMetricSpace.isometry_induced

theorem EMetricSpace.isometry_induced (f : Œ± ‚Üí Œ≤) (hf : f.Injective) [m : EMetricSpace Œ≤] :
    letI := m.induced f hf; Isometry f := fun _ _ ‚Ü¶ rfl

theorem MetricSpace.isometry_induced (f : Œ± ‚Üí Œ≤) (hf : f.Injective) [m : MetricSpace Œ≤] :
    letI := m.induced f hf; Isometry f := fun _ _ ‚Ü¶ rfl

/-- `IsometryClass F Œ± Œ≤` states that `F` is a type of isometries. -/
class IsometryClass (F : Type*) (Œ± Œ≤ : outParam Type*)
    [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] [FunLike F Œ± Œ≤] : Prop where
  protected isometry (f : F) : Isometry f

namespace IsometryClass

section PseudoEMetricSpace
variable [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤]

section
variable [FunLike F Œ± Œ≤] [IsometryClass F Œ± Œ≤] (f : F)

protected theorem edist_eq (x y : Œ±) : edist (f x) (f y) = edist x y :=
  (IsometryClass.isometry f).edist_eq x y

protected theorem continuous : Continuous f :=
  (IsometryClass.isometry f).continuous

protected theorem lipschitz : LipschitzWith 1 f :=
  (IsometryClass.isometry f).lipschitz

protected theorem antilipschitz : AntilipschitzWith 1 f :=
  (IsometryClass.isometry f).antilipschitz

theorem ediam_image (s : Set Œ±) : EMetric.diam (f '' s) = EMetric.diam s :=
  (IsometryClass.isometry f).ediam_image s

theorem ediam_range : EMetric.diam (range f) = EMetric.diam (univ : Set Œ±) :=
  (IsometryClass.isometry f).ediam_range

instance toContinuousMapClass : ContinuousMapClass F Œ± Œ≤ where
  map_continuous := IsometryClass.continuous

end

instance toHomeomorphClass [EquivLike F Œ± Œ≤] [IsometryClass F Œ± Œ≤] : HomeomorphClass F Œ± Œ≤ where
  map_continuous := IsometryClass.continuous
  inv_continuous f := ((IsometryClass.isometry f).right_inv (EquivLike.right_inv f)).continuous

end PseudoEMetricSpace

section PseudoMetricSpace
variable [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] [FunLike F Œ± Œ≤] [IsometryClass F Œ± Œ≤] (f : F)

protected theorem dist_eq (x y : Œ±) : dist (f x) (f y) = dist x y :=
  (IsometryClass.isometry f).dist_eq x y

protected theorem nndist_eq (x y : Œ±) : nndist (f x) (f y) = nndist x y :=
  (IsometryClass.isometry f).nndist_eq x y

theorem diam_image (s : Set Œ±) : Metric.diam (f '' s) = Metric.diam s :=
  (IsometryClass.isometry f).diam_image s

theorem diam_range : Metric.diam (range f) = Metric.diam (univ : Set Œ±) :=
  (IsometryClass.isometry f).diam_range

end PseudoMetricSpace

end IsometryClass

-- such a bijection need not exist
/-- `Œ±` and `Œ≤` are isometric if there is an isometric bijection between them. -/
structure IsometryEquiv (Œ± : Type u) (Œ≤ : Type v) [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤]
    extends Œ± ‚âÉ Œ≤ where
  isometry_toFun : Isometry toFun

@[inherit_doc]
infixl:25 " ‚âÉ·µ¢ " => IsometryEquiv

namespace IsometryEquiv

section PseudoEMetricSpace

variable [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] [PseudoEMetricSpace Œ≥]

theorem toEquiv_injective : Injective (toEquiv : (Œ± ‚âÉ·µ¢ Œ≤) ‚Üí (Œ± ‚âÉ Œ≤))
  | ‚ü®_, _‚ü©, ‚ü®_, _‚ü©, rfl => rfl

@[simp] theorem toEquiv_inj {e‚ÇÅ e‚ÇÇ : Œ± ‚âÉ·µ¢ Œ≤} : e‚ÇÅ.toEquiv = e‚ÇÇ.toEquiv ‚Üî e‚ÇÅ = e‚ÇÇ :=
  toEquiv_injective.eq_iff

instance : EquivLike (Œ± ‚âÉ·µ¢ Œ≤) Œ± Œ≤ where
  coe e := e.toEquiv
  inv e := e.toEquiv.symm
  left_inv e := e.left_inv
  right_inv e := e.right_inv
  coe_injective' _ _ h _ := toEquiv_injective <| DFunLike.ext' h

instance : IsometryClass (IsometryEquiv Œ± Œ≤) Œ± Œ≤ where
  isometry := isometry_toFun

theorem coe_eq_toEquiv (h : Œ± ‚âÉ·µ¢ Œ≤) (a : Œ±) : h a = h.toEquiv a := rfl

@[simp] theorem coe_toEquiv (h : Œ± ‚âÉ·µ¢ Œ≤) : ‚áëh.toEquiv = h := rfl

@[simp] theorem coe_mk (e : Œ± ‚âÉ Œ≤) (h) : ‚áë(mk e h) = e := rfl

protected theorem isometry (h : Œ± ‚âÉ·µ¢ Œ≤) : Isometry h :=
  h.isometry_toFun

protected theorem bijective (h : Œ± ‚âÉ·µ¢ Œ≤) : Bijective h :=
  h.toEquiv.bijective

protected theorem injective (h : Œ± ‚âÉ·µ¢ Œ≤) : Injective h :=
  h.toEquiv.injective

protected theorem surjective (h : Œ± ‚âÉ·µ¢ Œ≤) : Surjective h :=
  h.toEquiv.surjective

protected theorem edist_eq (h : Œ± ‚âÉ·µ¢ Œ≤) (x y : Œ±) : edist (h x) (h y) = edist x y :=
  h.isometry.edist_eq x y

protected theorem dist_eq {Œ± Œ≤ : Type*} [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] (h : Œ± ‚âÉ·µ¢ Œ≤)
    (x y : Œ±) : dist (h x) (h y) = dist x y :=
  h.isometry.dist_eq x y

protected theorem nndist_eq {Œ± Œ≤ : Type*} [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] (h : Œ± ‚âÉ·µ¢ Œ≤)
    (x y : Œ±) : nndist (h x) (h y) = nndist x y :=
  h.isometry.nndist_eq x y

protected theorem continuous (h : Œ± ‚âÉ·µ¢ Œ≤) : Continuous h :=
  h.isometry.continuous

@[simp]
theorem ediam_image (h : Œ± ‚âÉ·µ¢ Œ≤) (s : Set Œ±) : EMetric.diam (h '' s) = EMetric.diam s :=
  h.isometry.ediam_image s

@[ext]
theorem ext ‚¶Éh‚ÇÅ h‚ÇÇ : Œ± ‚âÉ·µ¢ Œ≤‚¶Ñ (H : ‚àÄ x, h‚ÇÅ x = h‚ÇÇ x) : h‚ÇÅ = h‚ÇÇ :=
  DFunLike.ext _ _ H

/-- Alternative constructor for isometric bijections,
taking as input an isometry, and a right inverse. -/
def mk' {Œ± : Type u} [EMetricSpace Œ±] (f : Œ± ‚Üí Œ≤) (g : Œ≤ ‚Üí Œ±) (hfg : ‚àÄ x, f (g x) = x)
    (hf : Isometry f) : Œ± ‚âÉ·µ¢ Œ≤ where
  toFun := f
  invFun := g
  left_inv _ := hf.injective <| hfg _
  right_inv := hfg
  isometry_toFun := hf

/-- The identity isometry of a space. -/
protected def refl (Œ± : Type*) [PseudoEMetricSpace Œ±] : Œ± ‚âÉ·µ¢ Œ± :=
  { Equiv.refl Œ± with isometry_toFun := isometry_id }

/-- The composition of two isometric isomorphisms, as an isometric isomorphism. -/
protected def trans (h‚ÇÅ : Œ± ‚âÉ·µ¢ Œ≤) (h‚ÇÇ : Œ≤ ‚âÉ·µ¢ Œ≥) : Œ± ‚âÉ·µ¢ Œ≥ :=
  { Equiv.trans h‚ÇÅ.toEquiv h‚ÇÇ.toEquiv with
    isometry_toFun := h‚ÇÇ.isometry_toFun.comp h‚ÇÅ.isometry_toFun }

@[simp]
theorem trans_apply (h‚ÇÅ : Œ± ‚âÉ·µ¢ Œ≤) (h‚ÇÇ : Œ≤ ‚âÉ·µ¢ Œ≥) (x : Œ±) : h‚ÇÅ.trans h‚ÇÇ x = h‚ÇÇ (h‚ÇÅ x) :=
  rfl

/-- The inverse of an isometric isomorphism, as an isometric isomorphism. -/
protected def symm (h : Œ± ‚âÉ·µ¢ Œ≤) : Œ≤ ‚âÉ·µ¢ Œ± where
  isometry_toFun := h.isometry.right_inv h.right_inv
  toEquiv := h.toEquiv.symm

/-- See Note [custom simps projection]. We need to specify this projection explicitly in this case,
  because it is a composition of multiple projections. -/
def Simps.apply (h : Œ± ‚âÉ·µ¢ Œ≤) : Œ± ‚Üí Œ≤ := h

/-- See Note [custom simps projection] -/
def Simps.symm_apply (h : Œ± ‚âÉ·µ¢ Œ≤) : Œ≤ ‚Üí Œ± :=
  h.symm

initialize_simps_projections IsometryEquiv (toFun ‚Üí apply, invFun ‚Üí symm_apply)

@[simp]
theorem symm_symm (h : Œ± ‚âÉ·µ¢ Œ≤) : h.symm.symm = h := rfl

theorem symm_bijective : Bijective (IsometryEquiv.symm : (Œ± ‚âÉ·µ¢ Œ≤) ‚Üí Œ≤ ‚âÉ·µ¢ Œ±) :=
  Function.bijective_iff_has_inverse.mpr ‚ü®_, symm_symm, symm_symm‚ü©

@[simp]
theorem apply_symm_apply (h : Œ± ‚âÉ·µ¢ Œ≤) (y : Œ≤) : h (h.symm y) = y :=
  h.toEquiv.apply_symm_apply y

@[simp]
theorem symm_apply_apply (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ±) : h.symm (h x) = x :=
  h.toEquiv.symm_apply_apply x

theorem symm_apply_eq (h : Œ± ‚âÉ·µ¢ Œ≤) {x : Œ±} {y : Œ≤} : h.symm y = x ‚Üî y = h x :=
  h.toEquiv.symm_apply_eq

theorem eq_symm_apply (h : Œ± ‚âÉ·µ¢ Œ≤) {x : Œ±} {y : Œ≤} : x = h.symm y ‚Üî h x = y :=
  h.toEquiv.eq_symm_apply

theorem symm_comp_self (h : Œ± ‚âÉ·µ¢ Œ≤) : (h.symm : Œ≤ ‚Üí Œ±) ‚àò h = id := funext h.left_inv

theorem self_comp_symm (h : Œ± ‚âÉ·µ¢ Œ≤) : (h : Œ± ‚Üí Œ≤) ‚àò h.symm = id := funext h.right_inv

theorem range_eq_univ (h : Œ± ‚âÉ·µ¢ Œ≤) : range h = univ := by simp

theorem image_symm (h : Œ± ‚âÉ·µ¢ Œ≤) : image h.symm = preimage h :=
  image_eq_preimage_of_inverse h.symm.toEquiv.left_inv h.symm.toEquiv.right_inv

theorem preimage_symm (h : Œ± ‚âÉ·µ¢ Œ≤) : preimage h.symm = image h :=
  (image_eq_preimage_of_inverse h.toEquiv.left_inv h.toEquiv.right_inv).symm

@[simp]
theorem symm_trans_apply (h‚ÇÅ : Œ± ‚âÉ·µ¢ Œ≤) (h‚ÇÇ : Œ≤ ‚âÉ·µ¢ Œ≥) (x : Œ≥) :
    (h‚ÇÅ.trans h‚ÇÇ).symm x = h‚ÇÅ.symm (h‚ÇÇ.symm x) :=
  rfl

theorem ediam_univ (h : Œ± ‚âÉ·µ¢ Œ≤) : EMetric.diam (univ : Set Œ±) = EMetric.diam (univ : Set Œ≤) := by
  rw [‚Üê h.range_eq_univ, h.isometry.ediam_range]

@[simp]
theorem ediam_preimage (h : Œ± ‚âÉ·µ¢ Œ≤) (s : Set Œ≤) : EMetric.diam (h ‚Åª¬π' s) = EMetric.diam s := by
  rw [‚Üê image_symm, ediam_image]

@[simp]
theorem preimage_emetric_ball (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ≤) (r : ‚Ñù‚â•0‚àû) :
    h ‚Åª¬π' EMetric.ball x r = EMetric.ball (h.symm x) r := by
  rw [‚Üê h.isometry.preimage_emetric_ball (h.symm x) r, h.apply_symm_apply]

@[simp]
theorem preimage_emetric_closedBall (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ≤) (r : ‚Ñù‚â•0‚àû) :
    h ‚Åª¬π' EMetric.closedBall x r = EMetric.closedBall (h.symm x) r := by
  rw [‚Üê h.isometry.preimage_emetric_closedBall (h.symm x) r, h.apply_symm_apply]

@[simp]
theorem image_emetric_ball (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ±) (r : ‚Ñù‚â•0‚àû) :
    h '' EMetric.ball x r = EMetric.ball (h x) r := by
  rw [‚Üê h.preimage_symm, h.symm.preimage_emetric_ball, symm_symm]

@[simp]
theorem image_emetric_closedBall (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ±) (r : ‚Ñù‚â•0‚àû) :
    h '' EMetric.closedBall x r = EMetric.closedBall (h x) r := by
  rw [‚Üê h.preimage_symm, h.symm.preimage_emetric_closedBall, symm_symm]

/-- The (bundled) homeomorphism associated to an isometric isomorphism. -/
@[simps toEquiv]
protected def toHomeomorph (h : Œ± ‚âÉ·µ¢ Œ≤) : Œ± ‚âÉ‚Çú Œ≤ where
  continuous_toFun := h.continuous
  continuous_invFun := h.symm.continuous
  toEquiv := h.toEquiv

@[simp]
theorem coe_toHomeomorph (h : Œ± ‚âÉ·µ¢ Œ≤) : ‚áëh.toHomeomorph = h :=
  rfl

@[simp]
theorem coe_toHomeomorph_symm (h : Œ± ‚âÉ·µ¢ Œ≤) : ‚áëh.toHomeomorph.symm = h.symm :=
  rfl

@[simp]
theorem comp_continuousOn_iff {Œ≥} [TopologicalSpace Œ≥] (h : Œ± ‚âÉ·µ¢ Œ≤) {f : Œ≥ ‚Üí Œ±} {s : Set Œ≥} :
    ContinuousOn (h ‚àò f) s ‚Üî ContinuousOn f s :=
  h.toHomeomorph.comp_continuousOn_iff _ _

@[simp]
theorem comp_continuous_iff {Œ≥} [TopologicalSpace Œ≥] (h : Œ± ‚âÉ·µ¢ Œ≤) {f : Œ≥ ‚Üí Œ±} :
    Continuous (h ‚àò f) ‚Üî Continuous f :=
  h.toHomeomorph.comp_continuous_iff

@[simp]
theorem comp_continuous_iff' {Œ≥} [TopologicalSpace Œ≥] (h : Œ± ‚âÉ·µ¢ Œ≤) {f : Œ≤ ‚Üí Œ≥} :
    Continuous (f ‚àò h) ‚Üî Continuous f :=
  h.toHomeomorph.comp_continuous_iff'

/-- The group of isometries. -/
instance : Group (Œ± ‚âÉ·µ¢ Œ±) where
  one := IsometryEquiv.refl _
  mul e‚ÇÅ e‚ÇÇ := e‚ÇÇ.trans e‚ÇÅ
  inv := IsometryEquiv.symm
  mul_assoc _ _ _ := rfl
  one_mul _ := ext fun _ => rfl
  mul_one _ := ext fun _ => rfl
  inv_mul_cancel e := ext e.symm_apply_apply

@[simp] theorem coe_one : ‚áë(1 : Œ± ‚âÉ·µ¢ Œ±) = id := rfl

@[simp] theorem coe_mul (e‚ÇÅ e‚ÇÇ : Œ± ‚âÉ·µ¢ Œ±) : ‚áë(e‚ÇÅ * e‚ÇÇ) = e‚ÇÅ ‚àò e‚ÇÇ := rfl

theorem mul_apply (e‚ÇÅ e‚ÇÇ : Œ± ‚âÉ·µ¢ Œ±) (x : Œ±) : (e‚ÇÅ * e‚ÇÇ) x = e‚ÇÅ (e‚ÇÇ x) := rfl

@[simp] theorem inv_apply_self (e : Œ± ‚âÉ·µ¢ Œ±) (x : Œ±) : e‚Åª¬π (e x) = x := e.symm_apply_apply x

@[simp] theorem apply_inv_self (e : Œ± ‚âÉ·µ¢ Œ±) (x : Œ±) : e (e‚Åª¬π x) = x := e.apply_symm_apply x

theorem completeSpace_iff (e : Œ± ‚âÉ·µ¢ Œ≤) : CompleteSpace Œ± ‚Üî CompleteSpace Œ≤ := by
  simp only [completeSpace_iff_isComplete_univ, ‚Üê e.range_eq_univ, ‚Üê image_univ,
    isComplete_image_iff e.isometry.isUniformInducing]

protected theorem completeSpace [CompleteSpace Œ≤] (e : Œ± ‚âÉ·µ¢ Œ≤) : CompleteSpace Œ± :=
  e.completeSpace_iff.2 ‚Äπ_‚Ä∫

/-- The natural isometry `‚àÄ i, Y i ‚âÉ·µ¢ ‚àÄ j, Y (e.symm j)` obtained from a bijection `Œπ ‚âÉ Œπ'` of
fintypes. `Equiv.piCongrLeft'` as an `IsometryEquiv`. -/
@[simps!]
def piCongrLeft' {Œπ' : Type*} [Fintype Œπ] [Fintype Œπ'] {Y : Œπ ‚Üí Type*}
    [‚àÄ j, PseudoEMetricSpace (Y j)] (e : Œπ ‚âÉ Œπ') : (‚àÄ i, Y i) ‚âÉ·µ¢ ‚àÄ j, Y (e.symm j) where
  toEquiv := Equiv.piCongrLeft' _ e
  isometry_toFun x1 x2 := by
    simp_rw [edist_pi_def, Finset.sup_univ_eq_iSup]
    exact (Equiv.iSup_comp (g := fun b ‚Ü¶ edist (x1 b) (x2 b)) e.symm)

/-- The natural isometry `‚àÄ i, Y (e i) ‚âÉ·µ¢ ‚àÄ j, Y j` obtained from a bijection `Œπ ‚âÉ Œπ'` of fintypes.
`Equiv.piCongrLeft` as an `IsometryEquiv`. -/
@[simps!]
def piCongrLeft {Œπ' : Type*} [Fintype Œπ] [Fintype Œπ'] {Y : Œπ' ‚Üí Type*}
    [‚àÄ j, PseudoEMetricSpace (Y j)] (e : Œπ ‚âÉ Œπ') : (‚àÄ i, Y (e i)) ‚âÉ·µ¢ ‚àÄ j, Y j :=
  (piCongrLeft' e.symm).symm

/-- The natural isometry `(Œ± ‚äï Œ≤ ‚Üí Œ≥) ‚âÉ·µ¢ (Œ± ‚Üí Œ≥) √ó (Œ≤ ‚Üí Œ≥)` between the type of maps on a sum of
fintypes `Œ± ‚äï Œ≤` and the pairs of functions on the types `Œ±` and `Œ≤`.
`Equiv.sumArrowEquivProdArrow` as an `IsometryEquiv`. -/
@[simps!]
def sumArrowIsometryEquivProdArrow [Fintype Œ±] [Fintype Œ≤] : (Œ± ‚äï Œ≤ ‚Üí Œ≥) ‚âÉ·µ¢ (Œ± ‚Üí Œ≥) √ó (Œ≤ ‚Üí Œ≥) where
  toEquiv := Equiv.sumArrowEquivProdArrow _ _ _
  isometry_toFun _ _ := by simp [Prod.edist_eq, edist_pi_def, Finset.sup_univ_eq_iSup, iSup_sum]

@[simp]
theorem sumArrowIsometryEquivProdArrow_toHomeomorph {Œ± Œ≤ : Type*} [Fintype Œ±] [Fintype Œ≤] :
    sumArrowIsometryEquivProdArrow.toHomeomorph
    = Homeomorph.sumArrowHomeomorphProdArrow (Œπ := Œ±) (Œπ' := Œ≤) (X := Œ≥) :=
  rfl

theorem _root_.Fin.edist_append_eq_max_edist (m n : ‚Ñï) {x x2 : Fin m ‚Üí Œ±} {y y2 : Fin n ‚Üí Œ±} :
    edist (Fin.append x y) (Fin.append x2 y2) = max (edist x x2) (edist y y2) := by
  simp [edist_pi_def, Finset.sup_univ_eq_iSup, ‚Üê Equiv.iSup_comp (e := finSumFinEquiv),
    iSup_sum]

/-- The natural `IsometryEquiv` between `(Fin m ‚Üí Œ±) √ó (Fin n ‚Üí Œ±)` and `Fin (m + n) ‚Üí Œ±`.
`Fin.appendEquiv` as an `IsometryEquiv`. -/
@[simps!]
def _root_.Fin.appendIsometry (m n : ‚Ñï) : (Fin m ‚Üí Œ±) √ó (Fin n ‚Üí Œ±) ‚âÉ·µ¢ (Fin (m + n) ‚Üí Œ±) where
  toEquiv := Fin.appendEquiv _ _
  isometry_toFun _ _ := by simp_rw [Fin.appendEquiv, Fin.edist_append_eq_max_edist, Prod.edist_eq]

@[simp]
theorem _root_.Fin.appendIsometry_toHomeomorph (m n : ‚Ñï) :
    (Fin.appendIsometry m n).toHomeomorph = Fin.appendHomeomorph (X := Œ±) m n :=
  rfl

/-- The natural `IsometryEquiv` `(Fin m ‚Üí ‚Ñù) √ó (Fin l ‚Üí ‚Ñù) ‚âÉ·µ¢ (Fin n ‚Üí ‚Ñù)` when `m + l = n`. -/
@[simps!]
def _root_.Fin.appendIsometryOfEq {n m l : ‚Ñï} (hmln : m + l = n) :
    (Fin m ‚Üí Œ±) √ó (Fin l ‚Üí Œ±) ‚âÉ·µ¢ (Fin n ‚Üí Œ±) :=
  (Fin.appendIsometry m l).trans (IsometryEquiv.piCongrLeft (Y := fun _ ‚Ü¶ Œ±) (finCongr hmln))

variable (Œπ Œ±)

/-- `Equiv.funUnique` as an `IsometryEquiv`. -/
@[simps!]
def funUnique [Unique Œπ] [Fintype Œπ] : (Œπ ‚Üí Œ±) ‚âÉ·µ¢ Œ± where
  toEquiv := Equiv.funUnique Œπ Œ±
  isometry_toFun x hx := by simp [edist_pi_def, Finset.univ_unique, Finset.sup_singleton]

/-- `piFinTwoEquiv` as an `IsometryEquiv`. -/
@[simps!]
def piFinTwo (Œ± : Fin 2 ‚Üí Type*) [‚àÄ i, PseudoEMetricSpace (Œ± i)] : (‚àÄ i, Œ± i) ‚âÉ·µ¢ Œ± 0 √ó Œ± 1 where
  toEquiv := piFinTwoEquiv Œ±
  isometry_toFun x hx := by simp [edist_pi_def, Fin.univ_succ, Prod.edist_eq]

end PseudoEMetricSpace

section PseudoMetricSpace

variable [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] (h : Œ± ‚âÉ·µ¢ Œ≤)

@[simp]
theorem diam_image (s : Set Œ±) : Metric.diam (h '' s) = Metric.diam s :=
  h.isometry.diam_image s

@[simp]
theorem diam_preimage (s : Set Œ≤) : Metric.diam (h ‚Åª¬π' s) = Metric.diam s := by
  rw [‚Üê image_symm, diam_image]

include h in
theorem diam_univ : Metric.diam (univ : Set Œ±) = Metric.diam (univ : Set Œ≤) :=
  congr_arg ENNReal.toReal h.ediam_univ

@[simp]
theorem preimage_ball (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ≤) (r : ‚Ñù) :
    h ‚Åª¬π' Metric.ball x r = Metric.ball (h.symm x) r := by
  rw [‚Üê h.isometry.preimage_ball (h.symm x) r, h.apply_symm_apply]

@[simp]
theorem preimage_sphere (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ≤) (r : ‚Ñù) :
    h ‚Åª¬π' Metric.sphere x r = Metric.sphere (h.symm x) r := by
  rw [‚Üê h.isometry.preimage_sphere (h.symm x) r, h.apply_symm_apply]

@[simp]
theorem preimage_closedBall (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ≤) (r : ‚Ñù) :
    h ‚Åª¬π' Metric.closedBall x r = Metric.closedBall (h.symm x) r := by
  rw [‚Üê h.isometry.preimage_closedBall (h.symm x) r, h.apply_symm_apply]

@[simp]
theorem image_ball (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ±) (r : ‚Ñù) : h '' Metric.ball x r = Metric.ball (h x) r := by
  rw [‚Üê h.preimage_symm, h.symm.preimage_ball, symm_symm]

@[simp]
theorem image_sphere (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ±) (r : ‚Ñù) :
    h '' Metric.sphere x r = Metric.sphere (h x) r := by
  rw [‚Üê h.preimage_symm, h.symm.preimage_sphere, symm_symm]

@[simp]
theorem image_closedBall (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ±) (r : ‚Ñù) :
    h '' Metric.closedBall x r = Metric.closedBall (h x) r := by
  rw [‚Üê h.preimage_symm, h.symm.preimage_closedBall, symm_symm]

end PseudoMetricSpace

end IsometryEquiv

/-- An isometry induces an isometric isomorphism between the source space and the
range of the isometry. -/
@[simps! +simpRhs toEquiv apply]
def Isometry.isometryEquivOnRange [EMetricSpace Œ±] [PseudoEMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤}
    (h : Isometry f) : Œ± ‚âÉ·µ¢ range f where
  isometry_toFun := h
  toEquiv := Equiv.ofInjective f h.injective

open NNReal in
/-- Post-composition by an isometry does not change the Lipschitz-property of a function. -/
lemma Isometry.lipschitzWith_iff {Œ± Œ≤ Œ≥ : Type*} [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤]
    [PseudoEMetricSpace Œ≥] {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ≥} (K : ‚Ñù‚â•0) (h : Isometry g) :
    LipschitzWith K (g ‚àò f) ‚Üî LipschitzWith K f := by
  simp [LipschitzWith, h.edist_eq]

namespace IsometryClass

variable [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] [EquivLike F Œ± Œ≤] [IsometryClass F Œ± Œ≤]

/-- Turn an element of a type `F` satisfying `EquivLike F Œ± Œ≤` and `IsometryClass F Œ± Œ≤` into
an actual `IsometryEquiv`. This is declared as the default coercion from `F` to `Œ± ‚âÉ·µ¢ Œ≤`. -/
@[coe]
def toIsometryEquiv (f : F) : Œ± ‚âÉ·µ¢ Œ≤ :=
  { (f : Œ± ‚âÉ Œ≤) with
    isometry_toFun := IsometryClass.isometry f }

@[simp]
theorem coe_coe (f : F) : ‚áë(toIsometryEquiv f) = ‚áëf := rfl

instance : CoeOut F (Œ± ‚âÉ·µ¢ Œ≤) :=
  ‚ü®toIsometryEquiv‚ü©

theorem toIsometryEquiv_injective : Function.Injective ((‚Üë) : F ‚Üí Œ± ‚âÉ·µ¢ Œ≤) :=
  fun _ _ e ‚Ü¶ DFunLike.ext _ _ fun a ‚Ü¶ DFunLike.congr_fun e a

end IsometryClass
