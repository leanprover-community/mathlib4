/-
Copyright (c) 2018 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Topology.MetricSpace.Antilipschitz

#align_import topology.metric_space.isometry from "leanprover-community/mathlib"@"b1859b6d4636fdbb78c5d5cefd24530653cfd3eb"

/-!
# Isometries

We define isometries, i.e., maps between emetric spaces that preserve
the edistance (on metric spaces, these are exactly the maps that preserve distances),
and prove their basic properties. We also introduce isometric bijections.

Since a lot of elementary properties don't require `eq_of_dist_eq_zero` we start setting up the
theory for `PseudoMetricSpace` and we specialize to `MetricSpace` when needed.
-/


noncomputable section

universe u v w

variable {Œπ : Type*} {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w}

open Function Set

open scoped Topology ENNReal

/-- An isometry (also known as isometric embedding) is a map preserving the edistance
between pseudoemetric spaces, or equivalently the distance between pseudometric space.  -/
def Isometry [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] (f : Œ± ‚Üí Œ≤) : Prop :=
  ‚àÄ x1 x2 : Œ±, edist (f x1) (f x2) = edist x1 x2
#align isometry Isometry

/-- On pseudometric spaces, a map is an isometry if and only if it preserves nonnegative
distances. -/
theorem isometry_iff_nndist_eq [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} :
    Isometry f ‚Üî ‚àÄ x y, nndist (f x) (f y) = nndist x y := by
  simp only [Isometry, edist_nndist, ENNReal.coe_inj]
#align isometry_iff_nndist_eq isometry_iff_nndist_eq

/-- On pseudometric spaces, a map is an isometry if and only if it preserves distances. -/
theorem isometry_iff_dist_eq [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} :
    Isometry f ‚Üî ‚àÄ x y, dist (f x) (f y) = dist x y := by
  simp only [isometry_iff_nndist_eq, ‚Üê coe_nndist, NNReal.coe_inj]
#align isometry_iff_dist_eq isometry_iff_dist_eq

/-- An isometry preserves distances. -/
alias ‚ü®Isometry.dist_eq, _‚ü© := isometry_iff_dist_eq
#align isometry.dist_eq Isometry.dist_eq

/-- A map that preserves distances is an isometry -/
alias ‚ü®_, Isometry.of_dist_eq‚ü© := isometry_iff_dist_eq
#align isometry.of_dist_eq Isometry.of_dist_eq

/-- An isometry preserves non-negative distances. -/
alias ‚ü®Isometry.nndist_eq, _‚ü© := isometry_iff_nndist_eq
#align isometry.nndist_eq Isometry.nndist_eq

/-- A map that preserves non-negative distances is an isometry. -/
alias ‚ü®_, Isometry.of_nndist_eq‚ü© := isometry_iff_nndist_eq
#align isometry.of_nndist_eq Isometry.of_nndist_eq

namespace Isometry

section PseudoEmetricIsometry

variable [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] [PseudoEMetricSpace Œ≥]
variable {f : Œ± ‚Üí Œ≤} {x y z : Œ±} {s : Set Œ±}

/-- An isometry preserves edistances. -/
theorem edist_eq (hf : Isometry f) (x y : Œ±) : edist (f x) (f y) = edist x y :=
  hf x y
#align isometry.edist_eq Isometry.edist_eq

theorem lipschitz (h : Isometry f) : LipschitzWith 1 f :=
  LipschitzWith.of_edist_le fun x y => (h x y).le
#align isometry.lipschitz Isometry.lipschitz

theorem antilipschitz (h : Isometry f) : AntilipschitzWith 1 f := fun x y => by
  simp only [h x y, ENNReal.coe_one, one_mul, le_refl]
#align isometry.antilipschitz Isometry.antilipschitz

/-- Any map on a subsingleton is an isometry -/
@[nontriviality]
theorem _root_.isometry_subsingleton [Subsingleton Œ±] : Isometry f := fun x y => by
  rw [Subsingleton.elim x y]; simp
#align isometry_subsingleton isometry_subsingleton

/-- The identity is an isometry -/
theorem _root_.isometry_id : Isometry (id : Œ± ‚Üí Œ±) := fun _ _ => rfl
#align isometry_id isometry_id

theorem prod_map {Œ¥} [PseudoEMetricSpace Œ¥] {f : Œ± ‚Üí Œ≤} {g : Œ≥ ‚Üí Œ¥} (hf : Isometry f)
    (hg : Isometry g) : Isometry (Prod.map f g) := fun x y => by
  simp only [Prod.edist_eq, hf.edist_eq, hg.edist_eq, Prod_map]
#align isometry.prod_map Isometry.prod_map

theorem _root_.isometry_dcomp {Œπ} [Fintype Œπ] {Œ± Œ≤ : Œπ ‚Üí Type*} [‚àÄ i, PseudoEMetricSpace (Œ± i)]
    [‚àÄ i, PseudoEMetricSpace (Œ≤ i)] (f : ‚àÄ i, Œ± i ‚Üí Œ≤ i) (hf : ‚àÄ i, Isometry (f i)) :
    Isometry (fun g : (i : Œπ) ‚Üí Œ± i => fun i => f i (g i)) := fun x y => by
  simp only [edist_pi_def, (hf _).edist_eq]
#align isometry_dcomp isometry_dcomp

/-- The composition of isometries is an isometry. -/
theorem comp {g : Œ≤ ‚Üí Œ≥} {f : Œ± ‚Üí Œ≤} (hg : Isometry g) (hf : Isometry f) : Isometry (g ‚àò f) :=
  fun _ _ => (hg _ _).trans (hf _ _)
#align isometry.comp Isometry.comp

/-- An isometry from a metric space is a uniform continuous map -/
protected theorem uniformContinuous (hf : Isometry f) : UniformContinuous f :=
  hf.lipschitz.uniformContinuous
#align isometry.uniform_continuous Isometry.uniformContinuous

/-- An isometry from a metric space is a uniform inducing map -/
protected theorem uniformInducing (hf : Isometry f) : UniformInducing f :=
  hf.antilipschitz.uniformInducing hf.uniformContinuous
#align isometry.uniform_inducing Isometry.uniformInducing

theorem tendsto_nhds_iff {Œπ : Type*} {f : Œ± ‚Üí Œ≤} {g : Œπ ‚Üí Œ±} {a : Filter Œπ} {b : Œ±}
    (hf : Isometry f) : Filter.Tendsto g a (ùìù b) ‚Üî Filter.Tendsto (f ‚àò g) a (ùìù (f b)) :=
  hf.uniformInducing.inducing.tendsto_nhds_iff
#align isometry.tendsto_nhds_iff Isometry.tendsto_nhds_iff

/-- An isometry is continuous. -/
protected theorem continuous (hf : Isometry f) : Continuous f :=
  hf.lipschitz.continuous
#align isometry.continuous Isometry.continuous

/-- The right inverse of an isometry is an isometry. -/
theorem right_inv {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ±} (h : Isometry f) (hg : RightInverse g f) : Isometry g :=
  fun x y => by rw [‚Üê h, hg _, hg _]
#align isometry.right_inv Isometry.right_inv

theorem preimage_emetric_closedBall (h : Isometry f) (x : Œ±) (r : ‚Ñù‚â•0‚àû) :
    f ‚Åª¬π' EMetric.closedBall (f x) r = EMetric.closedBall x r := by
  ext y
  simp [h.edist_eq]
#align isometry.preimage_emetric_closed_ball Isometry.preimage_emetric_closedBall

theorem preimage_emetric_ball (h : Isometry f) (x : Œ±) (r : ‚Ñù‚â•0‚àû) :
    f ‚Åª¬π' EMetric.ball (f x) r = EMetric.ball x r := by
  ext y
  simp [h.edist_eq]
#align isometry.preimage_emetric_ball Isometry.preimage_emetric_ball

/-- Isometries preserve the diameter in pseudoemetric spaces. -/
theorem ediam_image (hf : Isometry f) (s : Set Œ±) : EMetric.diam (f '' s) = EMetric.diam s :=
  eq_of_forall_ge_iff fun d => by simp only [EMetric.diam_le_iff, forall_mem_image, hf.edist_eq]
#align isometry.ediam_image Isometry.ediam_image

theorem ediam_range (hf : Isometry f) : EMetric.diam (range f) = EMetric.diam (univ : Set Œ±) := by
  rw [‚Üê image_univ]
  exact hf.ediam_image univ
#align isometry.ediam_range Isometry.ediam_range

theorem mapsTo_emetric_ball (hf : Isometry f) (x : Œ±) (r : ‚Ñù‚â•0‚àû) :
    MapsTo f (EMetric.ball x r) (EMetric.ball (f x) r) :=
  (hf.preimage_emetric_ball x r).ge
#align isometry.maps_to_emetric_ball Isometry.mapsTo_emetric_ball

theorem mapsTo_emetric_closedBall (hf : Isometry f) (x : Œ±) (r : ‚Ñù‚â•0‚àû) :
    MapsTo f (EMetric.closedBall x r) (EMetric.closedBall (f x) r) :=
  (hf.preimage_emetric_closedBall x r).ge
#align isometry.maps_to_emetric_closed_ball Isometry.mapsTo_emetric_closedBall

/-- The injection from a subtype is an isometry -/
theorem _root_.isometry_subtype_coe {s : Set Œ±} : Isometry ((‚Üë) : s ‚Üí Œ±) := fun _ _ => rfl
#align isometry_subtype_coe isometry_subtype_coe

theorem comp_continuousOn_iff {Œ≥} [TopologicalSpace Œ≥] (hf : Isometry f) {g : Œ≥ ‚Üí Œ±} {s : Set Œ≥} :
    ContinuousOn (f ‚àò g) s ‚Üî ContinuousOn g s :=
  hf.uniformInducing.inducing.continuousOn_iff.symm
#align isometry.comp_continuous_on_iff Isometry.comp_continuousOn_iff

theorem comp_continuous_iff {Œ≥} [TopologicalSpace Œ≥] (hf : Isometry f) {g : Œ≥ ‚Üí Œ±} :
    Continuous (f ‚àò g) ‚Üî Continuous g :=
  hf.uniformInducing.inducing.continuous_iff.symm
#align isometry.comp_continuous_iff Isometry.comp_continuous_iff

end PseudoEmetricIsometry

--section
section EmetricIsometry

variable [EMetricSpace Œ±] [PseudoEMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤}

/-- An isometry from an emetric space is injective -/
protected theorem injective (h : Isometry f) : Injective f :=
  h.antilipschitz.injective
#align isometry.injective Isometry.injective

/-- An isometry from an emetric space is a uniform embedding -/
protected theorem uniformEmbedding (hf : Isometry f) : UniformEmbedding f :=
  hf.antilipschitz.uniformEmbedding hf.lipschitz.uniformContinuous
#align isometry.uniform_embedding Isometry.uniformEmbedding

/-- An isometry from an emetric space is an embedding -/
protected theorem embedding (hf : Isometry f) : Embedding f :=
  hf.uniformEmbedding.embedding
#align isometry.embedding Isometry.embedding

/-- An isometry from a complete emetric space is a closed embedding -/
theorem closedEmbedding [CompleteSpace Œ±] [EMetricSpace Œ≥] {f : Œ± ‚Üí Œ≥} (hf : Isometry f) :
    ClosedEmbedding f :=
  hf.antilipschitz.closedEmbedding hf.lipschitz.uniformContinuous
#align isometry.closed_embedding Isometry.closedEmbedding

end EmetricIsometry

--section
section PseudoMetricIsometry

variable [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤}

/-- An isometry preserves the diameter in pseudometric spaces. -/
theorem diam_image (hf : Isometry f) (s : Set Œ±) : Metric.diam (f '' s) = Metric.diam s := by
  rw [Metric.diam, Metric.diam, hf.ediam_image]
#align isometry.diam_image Isometry.diam_image

theorem diam_range (hf : Isometry f) : Metric.diam (range f) = Metric.diam (univ : Set Œ±) := by
  rw [‚Üê image_univ]
  exact hf.diam_image univ
#align isometry.diam_range Isometry.diam_range

theorem preimage_setOf_dist (hf : Isometry f) (x : Œ±) (p : ‚Ñù ‚Üí Prop) :
    f ‚Åª¬π' { y | p (dist y (f x)) } = { y | p (dist y x) } := by
  ext y
  simp [hf.dist_eq]
#align isometry.preimage_set_of_dist Isometry.preimage_setOf_dist

theorem preimage_closedBall (hf : Isometry f) (x : Œ±) (r : ‚Ñù) :
    f ‚Åª¬π' Metric.closedBall (f x) r = Metric.closedBall x r :=
  hf.preimage_setOf_dist x (¬∑ ‚â§ r)
#align isometry.preimage_closed_ball Isometry.preimage_closedBall

theorem preimage_ball (hf : Isometry f) (x : Œ±) (r : ‚Ñù) :
    f ‚Åª¬π' Metric.ball (f x) r = Metric.ball x r :=
  hf.preimage_setOf_dist x (¬∑ < r)
#align isometry.preimage_ball Isometry.preimage_ball

theorem preimage_sphere (hf : Isometry f) (x : Œ±) (r : ‚Ñù) :
    f ‚Åª¬π' Metric.sphere (f x) r = Metric.sphere x r :=
  hf.preimage_setOf_dist x (¬∑ = r)
#align isometry.preimage_sphere Isometry.preimage_sphere

theorem mapsTo_ball (hf : Isometry f) (x : Œ±) (r : ‚Ñù) :
    MapsTo f (Metric.ball x r) (Metric.ball (f x) r) :=
  (hf.preimage_ball x r).ge
#align isometry.maps_to_ball Isometry.mapsTo_ball

theorem mapsTo_sphere (hf : Isometry f) (x : Œ±) (r : ‚Ñù) :
    MapsTo f (Metric.sphere x r) (Metric.sphere (f x) r) :=
  (hf.preimage_sphere x r).ge
#align isometry.maps_to_sphere Isometry.mapsTo_sphere

theorem mapsTo_closedBall (hf : Isometry f) (x : Œ±) (r : ‚Ñù) :
    MapsTo f (Metric.closedBall x r) (Metric.closedBall (f x) r) :=
  (hf.preimage_closedBall x r).ge
#align isometry.maps_to_closed_ball Isometry.mapsTo_closedBall

end PseudoMetricIsometry

-- section
end Isometry

-- namespace
/-- A uniform embedding from a uniform space to a metric space is an isometry with respect to the
induced metric space structure on the source space. -/
theorem UniformEmbedding.to_isometry {Œ± Œ≤} [UniformSpace Œ±] [MetricSpace Œ≤] {f : Œ± ‚Üí Œ≤}
    (h : UniformEmbedding f) : (letI := h.comapMetricSpace f; Isometry f) :=
  let _ := h.comapMetricSpace f
  Isometry.of_dist_eq fun _ _ => rfl
#align uniform_embedding.to_isometry UniformEmbedding.to_isometry

/-- An embedding from a topological space to a metric space is an isometry with respect to the
induced metric space structure on the source space. -/
theorem Embedding.to_isometry {Œ± Œ≤} [TopologicalSpace Œ±] [MetricSpace Œ≤] {f : Œ± ‚Üí Œ≤}
    (h : Embedding f) : (letI := h.comapMetricSpace f; Isometry f) :=
  let _ := h.comapMetricSpace f
  Isometry.of_dist_eq fun _ _ => rfl
#align embedding.to_isometry Embedding.to_isometry

-- such a bijection need not exist
/-- `Œ±` and `Œ≤` are isometric if there is an isometric bijection between them. -/
-- Porting note(#5171): was @[nolint has_nonempty_instance]
structure IsometryEquiv (Œ± : Type u) (Œ≤ : Type v) [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤]
    extends Œ± ‚âÉ Œ≤ where
  isometry_toFun : Isometry toFun
#align isometry_equiv IsometryEquiv

@[inherit_doc]
infixl:25 " ‚âÉ·µ¢ " => IsometryEquiv

namespace IsometryEquiv

section PseudoEMetricSpace

variable [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] [PseudoEMetricSpace Œ≥]

-- Porting note (#11215): TODO: add `IsometryEquivClass`

theorem toEquiv_injective : Injective (toEquiv : (Œ± ‚âÉ·µ¢ Œ≤) ‚Üí (Œ± ‚âÉ Œ≤))
  | ‚ü®_, _‚ü©, ‚ü®_, _‚ü©, rfl => rfl
#align isometry_equiv.to_equiv_inj IsometryEquiv.toEquiv_injective

@[simp] theorem toEquiv_inj {e‚ÇÅ e‚ÇÇ : Œ± ‚âÉ·µ¢ Œ≤} : e‚ÇÅ.toEquiv = e‚ÇÇ.toEquiv ‚Üî e‚ÇÅ = e‚ÇÇ :=
  toEquiv_injective.eq_iff

instance : EquivLike (Œ± ‚âÉ·µ¢ Œ≤) Œ± Œ≤ where
  coe e := e.toEquiv
  inv e := e.toEquiv.symm
  left_inv e := e.left_inv
  right_inv e := e.right_inv
  coe_injective' _ _ h _ := toEquiv_injective <| DFunLike.ext' h

theorem coe_eq_toEquiv (h : Œ± ‚âÉ·µ¢ Œ≤) (a : Œ±) : h a = h.toEquiv a := rfl
#align isometry_equiv.coe_eq_to_equiv IsometryEquiv.coe_eq_toEquiv

@[simp] theorem coe_toEquiv (h : Œ± ‚âÉ·µ¢ Œ≤) : ‚áëh.toEquiv = h := rfl
#align isometry_equiv.coe_to_equiv IsometryEquiv.coe_toEquiv

@[simp] theorem coe_mk (e : Œ± ‚âÉ Œ≤) (h) : ‚áë(mk e h) = e := rfl

protected theorem isometry (h : Œ± ‚âÉ·µ¢ Œ≤) : Isometry h :=
  h.isometry_toFun
#align isometry_equiv.isometry IsometryEquiv.isometry

protected theorem bijective (h : Œ± ‚âÉ·µ¢ Œ≤) : Bijective h :=
  h.toEquiv.bijective
#align isometry_equiv.bijective IsometryEquiv.bijective

protected theorem injective (h : Œ± ‚âÉ·µ¢ Œ≤) : Injective h :=
  h.toEquiv.injective
#align isometry_equiv.injective IsometryEquiv.injective

protected theorem surjective (h : Œ± ‚âÉ·µ¢ Œ≤) : Surjective h :=
  h.toEquiv.surjective
#align isometry_equiv.surjective IsometryEquiv.surjective

protected theorem edist_eq (h : Œ± ‚âÉ·µ¢ Œ≤) (x y : Œ±) : edist (h x) (h y) = edist x y :=
  h.isometry.edist_eq x y
#align isometry_equiv.edist_eq IsometryEquiv.edist_eq

protected theorem dist_eq {Œ± Œ≤ : Type*} [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] (h : Œ± ‚âÉ·µ¢ Œ≤)
    (x y : Œ±) : dist (h x) (h y) = dist x y :=
  h.isometry.dist_eq x y
#align isometry_equiv.dist_eq IsometryEquiv.dist_eq

protected theorem nndist_eq {Œ± Œ≤ : Type*} [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] (h : Œ± ‚âÉ·µ¢ Œ≤)
    (x y : Œ±) : nndist (h x) (h y) = nndist x y :=
  h.isometry.nndist_eq x y
#align isometry_equiv.nndist_eq IsometryEquiv.nndist_eq

protected theorem continuous (h : Œ± ‚âÉ·µ¢ Œ≤) : Continuous h :=
  h.isometry.continuous
#align isometry_equiv.continuous IsometryEquiv.continuous

@[simp]
theorem ediam_image (h : Œ± ‚âÉ·µ¢ Œ≤) (s : Set Œ±) : EMetric.diam (h '' s) = EMetric.diam s :=
  h.isometry.ediam_image s
#align isometry_equiv.ediam_image IsometryEquiv.ediam_image

@[ext]
theorem ext ‚¶Éh‚ÇÅ h‚ÇÇ : Œ± ‚âÉ·µ¢ Œ≤‚¶Ñ (H : ‚àÄ x, h‚ÇÅ x = h‚ÇÇ x) : h‚ÇÅ = h‚ÇÇ :=
  DFunLike.ext _ _ H
#align isometry_equiv.ext IsometryEquiv.ext

/-- Alternative constructor for isometric bijections,
taking as input an isometry, and a right inverse. -/
def mk' {Œ± : Type u} [EMetricSpace Œ±] (f : Œ± ‚Üí Œ≤) (g : Œ≤ ‚Üí Œ±) (hfg : ‚àÄ x, f (g x) = x)
    (hf : Isometry f) : Œ± ‚âÉ·µ¢ Œ≤ where
  toFun := f
  invFun := g
  left_inv _ := hf.injective <| hfg _
  right_inv := hfg
  isometry_toFun := hf
#align isometry_equiv.mk' IsometryEquiv.mk'

/-- The identity isometry of a space. -/
protected def refl (Œ± : Type*) [PseudoEMetricSpace Œ±] : Œ± ‚âÉ·µ¢ Œ± :=
  { Equiv.refl Œ± with isometry_toFun := isometry_id }
#align isometry_equiv.refl IsometryEquiv.refl

/-- The composition of two isometric isomorphisms, as an isometric isomorphism. -/
protected def trans (h‚ÇÅ : Œ± ‚âÉ·µ¢ Œ≤) (h‚ÇÇ : Œ≤ ‚âÉ·µ¢ Œ≥) : Œ± ‚âÉ·µ¢ Œ≥ :=
  { Equiv.trans h‚ÇÅ.toEquiv h‚ÇÇ.toEquiv with
    isometry_toFun := h‚ÇÇ.isometry_toFun.comp h‚ÇÅ.isometry_toFun }
#align isometry_equiv.trans IsometryEquiv.trans

@[simp]
theorem trans_apply (h‚ÇÅ : Œ± ‚âÉ·µ¢ Œ≤) (h‚ÇÇ : Œ≤ ‚âÉ·µ¢ Œ≥) (x : Œ±) : h‚ÇÅ.trans h‚ÇÇ x = h‚ÇÇ (h‚ÇÅ x) :=
  rfl
#align isometry_equiv.trans_apply IsometryEquiv.trans_apply

/-- The inverse of an isometric isomorphism, as an isometric isomorphism. -/
protected def symm (h : Œ± ‚âÉ·µ¢ Œ≤) : Œ≤ ‚âÉ·µ¢ Œ± where
  isometry_toFun := h.isometry.right_inv h.right_inv
  toEquiv := h.toEquiv.symm
#align isometry_equiv.symm IsometryEquiv.symm

/-- See Note [custom simps projection]. We need to specify this projection explicitly in this case,
  because it is a composition of multiple projections. -/
def Simps.apply (h : Œ± ‚âÉ·µ¢ Œ≤) : Œ± ‚Üí Œ≤ := h
#align isometry_equiv.simps.apply IsometryEquiv.Simps.apply

/-- See Note [custom simps projection] -/
def Simps.symm_apply (h : Œ± ‚âÉ·µ¢ Œ≤) : Œ≤ ‚Üí Œ± :=
  h.symm
#align isometry_equiv.simps.symm_apply IsometryEquiv.Simps.symm_apply

initialize_simps_projections IsometryEquiv (toEquiv_toFun ‚Üí apply, toEquiv_invFun ‚Üí symm_apply)

@[simp]
theorem symm_symm (h : Œ± ‚âÉ·µ¢ Œ≤) : h.symm.symm = h := rfl
#align isometry_equiv.symm_symm IsometryEquiv.symm_symm

theorem symm_bijective : Bijective (IsometryEquiv.symm : (Œ± ‚âÉ·µ¢ Œ≤) ‚Üí Œ≤ ‚âÉ·µ¢ Œ±) :=
  Function.bijective_iff_has_inverse.mpr ‚ü®_, symm_symm, symm_symm‚ü©

@[simp]
theorem apply_symm_apply (h : Œ± ‚âÉ·µ¢ Œ≤) (y : Œ≤) : h (h.symm y) = y :=
  h.toEquiv.apply_symm_apply y
#align isometry_equiv.apply_symm_apply IsometryEquiv.apply_symm_apply

@[simp]
theorem symm_apply_apply (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ±) : h.symm (h x) = x :=
  h.toEquiv.symm_apply_apply x
#align isometry_equiv.symm_apply_apply IsometryEquiv.symm_apply_apply

theorem symm_apply_eq (h : Œ± ‚âÉ·µ¢ Œ≤) {x : Œ±} {y : Œ≤} : h.symm y = x ‚Üî y = h x :=
  h.toEquiv.symm_apply_eq
#align isometry_equiv.symm_apply_eq IsometryEquiv.symm_apply_eq

theorem eq_symm_apply (h : Œ± ‚âÉ·µ¢ Œ≤) {x : Œ±} {y : Œ≤} : x = h.symm y ‚Üî h x = y :=
  h.toEquiv.eq_symm_apply
#align isometry_equiv.eq_symm_apply IsometryEquiv.eq_symm_apply

theorem symm_comp_self (h : Œ± ‚âÉ·µ¢ Œ≤) : (h.symm : Œ≤ ‚Üí Œ±) ‚àò h = id := funext h.left_inv
#align isometry_equiv.symm_comp_self IsometryEquiv.symm_comp_self

theorem self_comp_symm (h : Œ± ‚âÉ·µ¢ Œ≤) : (h : Œ± ‚Üí Œ≤) ‚àò h.symm = id := funext h.right_inv
#align isometry_equiv.self_comp_symm IsometryEquiv.self_comp_symm

@[simp]
theorem range_eq_univ (h : Œ± ‚âÉ·µ¢ Œ≤) : range h = univ :=
  h.toEquiv.range_eq_univ
#align isometry_equiv.range_eq_univ IsometryEquiv.range_eq_univ

theorem image_symm (h : Œ± ‚âÉ·µ¢ Œ≤) : image h.symm = preimage h :=
  image_eq_preimage_of_inverse h.symm.toEquiv.left_inv h.symm.toEquiv.right_inv
#align isometry_equiv.image_symm IsometryEquiv.image_symm

theorem preimage_symm (h : Œ± ‚âÉ·µ¢ Œ≤) : preimage h.symm = image h :=
  (image_eq_preimage_of_inverse h.toEquiv.left_inv h.toEquiv.right_inv).symm
#align isometry_equiv.preimage_symm IsometryEquiv.preimage_symm

@[simp]
theorem symm_trans_apply (h‚ÇÅ : Œ± ‚âÉ·µ¢ Œ≤) (h‚ÇÇ : Œ≤ ‚âÉ·µ¢ Œ≥) (x : Œ≥) :
    (h‚ÇÅ.trans h‚ÇÇ).symm x = h‚ÇÅ.symm (h‚ÇÇ.symm x) :=
  rfl
#align isometry_equiv.symm_trans_apply IsometryEquiv.symm_trans_apply

theorem ediam_univ (h : Œ± ‚âÉ·µ¢ Œ≤) : EMetric.diam (univ : Set Œ±) = EMetric.diam (univ : Set Œ≤) := by
  rw [‚Üê h.range_eq_univ, h.isometry.ediam_range]
#align isometry_equiv.ediam_univ IsometryEquiv.ediam_univ

@[simp]
theorem ediam_preimage (h : Œ± ‚âÉ·µ¢ Œ≤) (s : Set Œ≤) : EMetric.diam (h ‚Åª¬π' s) = EMetric.diam s := by
  rw [‚Üê image_symm, ediam_image]
#align isometry_equiv.ediam_preimage IsometryEquiv.ediam_preimage

@[simp]
theorem preimage_emetric_ball (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ≤) (r : ‚Ñù‚â•0‚àû) :
    h ‚Åª¬π' EMetric.ball x r = EMetric.ball (h.symm x) r := by
  rw [‚Üê h.isometry.preimage_emetric_ball (h.symm x) r, h.apply_symm_apply]
#align isometry_equiv.preimage_emetric_ball IsometryEquiv.preimage_emetric_ball

@[simp]
theorem preimage_emetric_closedBall (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ≤) (r : ‚Ñù‚â•0‚àû) :
    h ‚Åª¬π' EMetric.closedBall x r = EMetric.closedBall (h.symm x) r := by
  rw [‚Üê h.isometry.preimage_emetric_closedBall (h.symm x) r, h.apply_symm_apply]
#align isometry_equiv.preimage_emetric_closed_ball IsometryEquiv.preimage_emetric_closedBall

@[simp]
theorem image_emetric_ball (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ±) (r : ‚Ñù‚â•0‚àû) :
    h '' EMetric.ball x r = EMetric.ball (h x) r := by
  rw [‚Üê h.preimage_symm, h.symm.preimage_emetric_ball, symm_symm]
#align isometry_equiv.image_emetric_ball IsometryEquiv.image_emetric_ball

@[simp]
theorem image_emetric_closedBall (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ±) (r : ‚Ñù‚â•0‚àû) :
    h '' EMetric.closedBall x r = EMetric.closedBall (h x) r := by
  rw [‚Üê h.preimage_symm, h.symm.preimage_emetric_closedBall, symm_symm]
#align isometry_equiv.image_emetric_closed_ball IsometryEquiv.image_emetric_closedBall

/-- The (bundled) homeomorphism associated to an isometric isomorphism. -/
@[simps toEquiv]
protected def toHomeomorph (h : Œ± ‚âÉ·µ¢ Œ≤) : Œ± ‚âÉ‚Çú Œ≤ where
  continuous_toFun := h.continuous
  continuous_invFun := h.symm.continuous
  toEquiv := h.toEquiv
#align isometry_equiv.to_homeomorph IsometryEquiv.toHomeomorph
#align isometry_equiv.to_homeomorph_to_equiv IsometryEquiv.toHomeomorph_toEquiv

@[simp]
theorem coe_toHomeomorph (h : Œ± ‚âÉ·µ¢ Œ≤) : ‚áëh.toHomeomorph = h :=
  rfl
#align isometry_equiv.coe_to_homeomorph IsometryEquiv.coe_toHomeomorph

@[simp]
theorem coe_toHomeomorph_symm (h : Œ± ‚âÉ·µ¢ Œ≤) : ‚áëh.toHomeomorph.symm = h.symm :=
  rfl
#align isometry_equiv.coe_to_homeomorph_symm IsometryEquiv.coe_toHomeomorph_symm

@[simp]
theorem comp_continuousOn_iff {Œ≥} [TopologicalSpace Œ≥] (h : Œ± ‚âÉ·µ¢ Œ≤) {f : Œ≥ ‚Üí Œ±} {s : Set Œ≥} :
    ContinuousOn (h ‚àò f) s ‚Üî ContinuousOn f s :=
  h.toHomeomorph.comp_continuousOn_iff _ _
#align isometry_equiv.comp_continuous_on_iff IsometryEquiv.comp_continuousOn_iff

@[simp]
theorem comp_continuous_iff {Œ≥} [TopologicalSpace Œ≥] (h : Œ± ‚âÉ·µ¢ Œ≤) {f : Œ≥ ‚Üí Œ±} :
    Continuous (h ‚àò f) ‚Üî Continuous f :=
  h.toHomeomorph.comp_continuous_iff
#align isometry_equiv.comp_continuous_iff IsometryEquiv.comp_continuous_iff

@[simp]
theorem comp_continuous_iff' {Œ≥} [TopologicalSpace Œ≥] (h : Œ± ‚âÉ·µ¢ Œ≤) {f : Œ≤ ‚Üí Œ≥} :
    Continuous (f ‚àò h) ‚Üî Continuous f :=
  h.toHomeomorph.comp_continuous_iff'
#align isometry_equiv.comp_continuous_iff' IsometryEquiv.comp_continuous_iff'

/-- The group of isometries. -/
instance : Group (Œ± ‚âÉ·µ¢ Œ±) where
  one := IsometryEquiv.refl _
  mul e‚ÇÅ e‚ÇÇ := e‚ÇÇ.trans e‚ÇÅ
  inv := IsometryEquiv.symm
  mul_assoc e‚ÇÅ e‚ÇÇ e‚ÇÉ := rfl
  one_mul e := ext fun _ => rfl
  mul_one e := ext fun _ => rfl
  mul_left_inv e := ext e.symm_apply_apply

@[simp] theorem coe_one : ‚áë(1 : Œ± ‚âÉ·µ¢ Œ±) = id := rfl
#align isometry_equiv.coe_one IsometryEquiv.coe_one

@[simp] theorem coe_mul (e‚ÇÅ e‚ÇÇ : Œ± ‚âÉ·µ¢ Œ±) : ‚áë(e‚ÇÅ * e‚ÇÇ) = e‚ÇÅ ‚àò e‚ÇÇ := rfl
#align isometry_equiv.coe_mul IsometryEquiv.coe_mul

theorem mul_apply (e‚ÇÅ e‚ÇÇ : Œ± ‚âÉ·µ¢ Œ±) (x : Œ±) : (e‚ÇÅ * e‚ÇÇ) x = e‚ÇÅ (e‚ÇÇ x) := rfl
#align isometry_equiv.mul_apply IsometryEquiv.mul_apply

@[simp] theorem inv_apply_self (e : Œ± ‚âÉ·µ¢ Œ±) (x : Œ±) : e‚Åª¬π (e x) = x := e.symm_apply_apply x
#align isometry_equiv.inv_apply_self IsometryEquiv.inv_apply_self

@[simp] theorem apply_inv_self (e : Œ± ‚âÉ·µ¢ Œ±) (x : Œ±) : e (e‚Åª¬π x) = x := e.apply_symm_apply x
#align isometry_equiv.apply_inv_self IsometryEquiv.apply_inv_self

theorem completeSpace_iff (e : Œ± ‚âÉ·µ¢ Œ≤) : CompleteSpace Œ± ‚Üî CompleteSpace Œ≤ := by
  simp only [completeSpace_iff_isComplete_univ, ‚Üê e.range_eq_univ, ‚Üê image_univ,
    isComplete_image_iff e.isometry.uniformInducing]
#align isometry_equiv.complete_space_iff IsometryEquiv.completeSpace_iff

protected theorem completeSpace [CompleteSpace Œ≤] (e : Œ± ‚âÉ·µ¢ Œ≤) : CompleteSpace Œ± :=
  e.completeSpace_iff.2 ‚Äπ_‚Ä∫
#align isometry_equiv.complete_space IsometryEquiv.completeSpace

variable (Œπ Œ±)

/-- `Equiv.funUnique` as an `IsometryEquiv`. -/
@[simps!]
def funUnique [Unique Œπ] [Fintype Œπ] : (Œπ ‚Üí Œ±) ‚âÉ·µ¢ Œ± where
  toEquiv := Equiv.funUnique Œπ Œ±
  isometry_toFun x hx := by simp [edist_pi_def, Finset.univ_unique, Finset.sup_singleton]
#align isometry_equiv.fun_unique IsometryEquiv.funUnique

/-- `piFinTwoEquiv` as an `IsometryEquiv`. -/
@[simps!]
def piFinTwo (Œ± : Fin 2 ‚Üí Type*) [‚àÄ i, PseudoEMetricSpace (Œ± i)] : (‚àÄ i, Œ± i) ‚âÉ·µ¢ Œ± 0 √ó Œ± 1 where
  toEquiv := piFinTwoEquiv Œ±
  isometry_toFun x hx := by simp [edist_pi_def, Fin.univ_succ, Prod.edist_eq]
#align isometry_equiv.pi_fin_two IsometryEquiv.piFinTwo

end PseudoEMetricSpace

section PseudoMetricSpace

variable [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] (h : Œ± ‚âÉ·µ¢ Œ≤)

@[simp]
theorem diam_image (s : Set Œ±) : Metric.diam (h '' s) = Metric.diam s :=
  h.isometry.diam_image s
#align isometry_equiv.diam_image IsometryEquiv.diam_image

@[simp]
theorem diam_preimage (s : Set Œ≤) : Metric.diam (h ‚Åª¬π' s) = Metric.diam s := by
  rw [‚Üê image_symm, diam_image]
#align isometry_equiv.diam_preimage IsometryEquiv.diam_preimage

theorem diam_univ : Metric.diam (univ : Set Œ±) = Metric.diam (univ : Set Œ≤) :=
  congr_arg ENNReal.toReal h.ediam_univ
#align isometry_equiv.diam_univ IsometryEquiv.diam_univ

@[simp]
theorem preimage_ball (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ≤) (r : ‚Ñù) :
    h ‚Åª¬π' Metric.ball x r = Metric.ball (h.symm x) r := by
  rw [‚Üê h.isometry.preimage_ball (h.symm x) r, h.apply_symm_apply]
#align isometry_equiv.preimage_ball IsometryEquiv.preimage_ball

@[simp]
theorem preimage_sphere (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ≤) (r : ‚Ñù) :
    h ‚Åª¬π' Metric.sphere x r = Metric.sphere (h.symm x) r := by
  rw [‚Üê h.isometry.preimage_sphere (h.symm x) r, h.apply_symm_apply]
#align isometry_equiv.preimage_sphere IsometryEquiv.preimage_sphere

@[simp]
theorem preimage_closedBall (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ≤) (r : ‚Ñù) :
    h ‚Åª¬π' Metric.closedBall x r = Metric.closedBall (h.symm x) r := by
  rw [‚Üê h.isometry.preimage_closedBall (h.symm x) r, h.apply_symm_apply]
#align isometry_equiv.preimage_closed_ball IsometryEquiv.preimage_closedBall

@[simp]
theorem image_ball (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ±) (r : ‚Ñù) : h '' Metric.ball x r = Metric.ball (h x) r := by
  rw [‚Üê h.preimage_symm, h.symm.preimage_ball, symm_symm]
#align isometry_equiv.image_ball IsometryEquiv.image_ball

@[simp]
theorem image_sphere (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ±) (r : ‚Ñù) :
    h '' Metric.sphere x r = Metric.sphere (h x) r := by
  rw [‚Üê h.preimage_symm, h.symm.preimage_sphere, symm_symm]
#align isometry_equiv.image_sphere IsometryEquiv.image_sphere

@[simp]
theorem image_closedBall (h : Œ± ‚âÉ·µ¢ Œ≤) (x : Œ±) (r : ‚Ñù) :
    h '' Metric.closedBall x r = Metric.closedBall (h x) r := by
  rw [‚Üê h.preimage_symm, h.symm.preimage_closedBall, symm_symm]
#align isometry_equiv.image_closed_ball IsometryEquiv.image_closedBall

end PseudoMetricSpace

end IsometryEquiv

/-- An isometry induces an isometric isomorphism between the source space and the
range of the isometry. -/
@[simps! (config := { simpRhs := true }) toEquiv apply]
def Isometry.isometryEquivOnRange [EMetricSpace Œ±] [PseudoEMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤}
    (h : Isometry f) : Œ± ‚âÉ·µ¢ range f where
  isometry_toFun := h
  toEquiv := Equiv.ofInjective f h.injective
#align isometry.isometry_equiv_on_range Isometry.isometryEquivOnRange
#align isometry.isometry_equiv_on_range_to_equiv Isometry.isometryEquivOnRange_toEquiv
#align isometry.isometry_equiv_on_range_apply Isometry.isometryEquivOnRange_apply

open NNReal in
/-- Post-composition by an isometry does not change the Lipschitz-property of a function. -/
lemma Isometry.lipschitzWith_iff {Œ± Œ≤ Œ≥ : Type*} [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤]
    [PseudoEMetricSpace Œ≥] {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ≥} (K : ‚Ñù‚â•0) (h : Isometry g) :
    LipschitzWith K (g ‚àò f) ‚Üî LipschitzWith K f := by
  simp [LipschitzWith, h.edist_eq]
