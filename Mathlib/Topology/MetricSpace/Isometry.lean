/-
Copyright (c) 2018 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Isometries of emetric and metric spaces
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Topology.MetricSpace.Antilipschitz

#align_import topology.metric_space.isometry from "leanprover-community/mathlib"@"b1859b6d4636fdbb78c5d5cefd24530653cfd3eb"

/-!
# Isometries

We define isometries, i.e., maps between emetric spaces that preserve
the edistance (on metric spaces, these are exactly the maps that preserve distances),
and prove their basic properties. We also introduce isometric bijections.

Since a lot of elementary properties don't require `eq_of_dist_eq_zero` we start setting up the
theory for `PseudoMetricSpace` and we specialize to `MetricSpace` when needed.
-/


noncomputable section

universe u v w

variable {Î¹ : Type*} {Î± : Type u} {Î² : Type v} {Î³ : Type w}

open Function Set

open scoped Topology ENNReal

/-- An isometry (also known as isometric embedding) is a map preserving the edistance
between pseudoemetric spaces, or equivalently the distance between pseudometric space.  -/
def Isometry [PseudoEMetricSpace Î±] [PseudoEMetricSpace Î²] (f : Î± â†’ Î²) : Prop :=
  âˆ€ x1 x2 : Î±, edist (f x1) (f x2) = edist x1 x2
#align isometry Isometry

/-- On pseudometric spaces, a map is an isometry if and only if it preserves nonnegative
distances. -/
theorem isometry_iff_nndist_eq [PseudoMetricSpace Î±] [PseudoMetricSpace Î²] {f : Î± â†’ Î²} :
    Isometry f â†” âˆ€ x y, nndist (f x) (f y) = nndist x y := by
  simp only [Isometry, edist_nndist, ENNReal.coe_eq_coe]
  -- ğŸ‰ no goals
#align isometry_iff_nndist_eq isometry_iff_nndist_eq

/-- On pseudometric spaces, a map is an isometry if and only if it preserves distances. -/
theorem isometry_iff_dist_eq [PseudoMetricSpace Î±] [PseudoMetricSpace Î²] {f : Î± â†’ Î²} :
    Isometry f â†” âˆ€ x y, dist (f x) (f y) = dist x y := by
  simp only [isometry_iff_nndist_eq, â† coe_nndist, NNReal.coe_eq]
  -- ğŸ‰ no goals
#align isometry_iff_dist_eq isometry_iff_dist_eq

/-- An isometry preserves distances. -/
alias âŸ¨Isometry.dist_eq, _âŸ© := isometry_iff_dist_eq
#align isometry.dist_eq Isometry.dist_eq

/-- A map that preserves distances is an isometry -/
alias âŸ¨_, Isometry.of_dist_eqâŸ© := isometry_iff_dist_eq
#align isometry.of_dist_eq Isometry.of_dist_eq

/-- An isometry preserves non-negative distances. -/
alias âŸ¨Isometry.nndist_eq, _âŸ© := isometry_iff_nndist_eq
#align isometry.nndist_eq Isometry.nndist_eq

/-- A map that preserves non-negative distances is an isometry. -/
alias âŸ¨_, Isometry.of_nndist_eqâŸ© := isometry_iff_nndist_eq
#align isometry.of_nndist_eq Isometry.of_nndist_eq

namespace Isometry

section PseudoEmetricIsometry

variable [PseudoEMetricSpace Î±] [PseudoEMetricSpace Î²] [PseudoEMetricSpace Î³]

variable {f : Î± â†’ Î²} {x y z : Î±} {s : Set Î±}

/-- An isometry preserves edistances. -/
theorem edist_eq (hf : Isometry f) (x y : Î±) : edist (f x) (f y) = edist x y :=
  hf x y
#align isometry.edist_eq Isometry.edist_eq

theorem lipschitz (h : Isometry f) : LipschitzWith 1 f :=
  LipschitzWith.of_edist_le fun x y => (h x y).le
#align isometry.lipschitz Isometry.lipschitz

theorem antilipschitz (h : Isometry f) : AntilipschitzWith 1 f := fun x y => by
  simp only [h x y, ENNReal.coe_one, one_mul, le_refl]
  -- ğŸ‰ no goals
#align isometry.antilipschitz Isometry.antilipschitz

/-- Any map on a subsingleton is an isometry -/
@[nontriviality]
theorem _root_.isometry_subsingleton [Subsingleton Î±] : Isometry f := fun x y => by
  rw [Subsingleton.elim x y]; simp
  -- âŠ¢ edist (f y) (f y) = edist y y
                              -- ğŸ‰ no goals
#align isometry_subsingleton isometry_subsingleton

/-- The identity is an isometry -/
theorem _root_.isometry_id : Isometry (id : Î± â†’ Î±) := fun _ _ => rfl
#align isometry_id isometry_id

theorem prod_map {Î´} [PseudoEMetricSpace Î´] {f : Î± â†’ Î²} {g : Î³ â†’ Î´} (hf : Isometry f)
    (hg : Isometry g) : Isometry (Prod.map f g) := fun x y => by
  simp only [Prod.edist_eq, hf.edist_eq, hg.edist_eq, Prod_map]
  -- ğŸ‰ no goals
#align isometry.prod_map Isometry.prod_map

theorem _root_.isometry_dcomp {Î¹} [Fintype Î¹] {Î± Î² : Î¹ â†’ Type*} [âˆ€ i, PseudoEMetricSpace (Î± i)]
    [âˆ€ i, PseudoEMetricSpace (Î² i)] (f : âˆ€ i, Î± i â†’ Î² i) (hf : âˆ€ i, Isometry (f i)) :
    Isometry (fun g : (i : Î¹) â†’ Î± i => fun i => f i (g i)) := fun x y => by
  simp only [edist_pi_def, (hf _).edist_eq]
  -- ğŸ‰ no goals
#align isometry_dcomp isometry_dcomp

/-- The composition of isometries is an isometry. -/
theorem comp {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : Isometry g) (hf : Isometry f) : Isometry (g âˆ˜ f) :=
  fun _ _ => (hg _ _).trans (hf _ _)
#align isometry.comp Isometry.comp

/-- An isometry from a metric space is a uniform continuous map -/
protected theorem uniformContinuous (hf : Isometry f) : UniformContinuous f :=
  hf.lipschitz.uniformContinuous
#align isometry.uniform_continuous Isometry.uniformContinuous

/-- An isometry from a metric space is a uniform inducing map -/
protected theorem uniformInducing (hf : Isometry f) : UniformInducing f :=
  hf.antilipschitz.uniformInducing hf.uniformContinuous
#align isometry.uniform_inducing Isometry.uniformInducing

theorem tendsto_nhds_iff {Î¹ : Type*} {f : Î± â†’ Î²} {g : Î¹ â†’ Î±} {a : Filter Î¹} {b : Î±}
    (hf : Isometry f) : Filter.Tendsto g a (ğ“ b) â†” Filter.Tendsto (f âˆ˜ g) a (ğ“ (f b)) :=
  hf.uniformInducing.inducing.tendsto_nhds_iff
#align isometry.tendsto_nhds_iff Isometry.tendsto_nhds_iff

/-- An isometry is continuous. -/
protected theorem continuous (hf : Isometry f) : Continuous f :=
  hf.lipschitz.continuous
#align isometry.continuous Isometry.continuous

/-- The right inverse of an isometry is an isometry. -/
theorem right_inv {f : Î± â†’ Î²} {g : Î² â†’ Î±} (h : Isometry f) (hg : RightInverse g f) : Isometry g :=
  fun x y => by rw [â† h, hg _, hg _]
                -- ğŸ‰ no goals
#align isometry.right_inv Isometry.right_inv

theorem preimage_emetric_closedBall (h : Isometry f) (x : Î±) (r : â„â‰¥0âˆ) :
    f â»Â¹' EMetric.closedBall (f x) r = EMetric.closedBall x r := by
  ext y
  -- âŠ¢ y âˆˆ f â»Â¹' EMetric.closedBall (f x) r â†” y âˆˆ EMetric.closedBall x r
  simp [h.edist_eq]
  -- ğŸ‰ no goals
#align isometry.preimage_emetric_closed_ball Isometry.preimage_emetric_closedBall

theorem preimage_emetric_ball (h : Isometry f) (x : Î±) (r : â„â‰¥0âˆ) :
    f â»Â¹' EMetric.ball (f x) r = EMetric.ball x r := by
  ext y
  -- âŠ¢ y âˆˆ f â»Â¹' EMetric.ball (f x) r â†” y âˆˆ EMetric.ball x r
  simp [h.edist_eq]
  -- ğŸ‰ no goals
#align isometry.preimage_emetric_ball Isometry.preimage_emetric_ball

/-- Isometries preserve the diameter in pseudoemetric spaces. -/
theorem ediam_image (hf : Isometry f) (s : Set Î±) : EMetric.diam (f '' s) = EMetric.diam s :=
  eq_of_forall_ge_iff fun d => by simp only [EMetric.diam_le_iff, ball_image_iff, hf.edist_eq]
                                  -- ğŸ‰ no goals
#align isometry.ediam_image Isometry.ediam_image

theorem ediam_range (hf : Isometry f) : EMetric.diam (range f) = EMetric.diam (univ : Set Î±) := by
  rw [â† image_univ]
  -- âŠ¢ EMetric.diam (f '' univ) = EMetric.diam univ
  exact hf.ediam_image univ
  -- ğŸ‰ no goals
#align isometry.ediam_range Isometry.ediam_range

theorem mapsTo_emetric_ball (hf : Isometry f) (x : Î±) (r : â„â‰¥0âˆ) :
    MapsTo f (EMetric.ball x r) (EMetric.ball (f x) r) :=
  (hf.preimage_emetric_ball x r).ge
#align isometry.maps_to_emetric_ball Isometry.mapsTo_emetric_ball

theorem mapsTo_emetric_closedBall (hf : Isometry f) (x : Î±) (r : â„â‰¥0âˆ) :
    MapsTo f (EMetric.closedBall x r) (EMetric.closedBall (f x) r) :=
  (hf.preimage_emetric_closedBall x r).ge
#align isometry.maps_to_emetric_closed_ball Isometry.mapsTo_emetric_closedBall

/-- The injection from a subtype is an isometry -/
theorem _root_.isometry_subtype_coe {s : Set Î±} : Isometry ((â†‘) : s â†’ Î±) := fun _ _ => rfl
#align isometry_subtype_coe isometry_subtype_coe

theorem comp_continuousOn_iff {Î³} [TopologicalSpace Î³] (hf : Isometry f) {g : Î³ â†’ Î±} {s : Set Î³} :
    ContinuousOn (f âˆ˜ g) s â†” ContinuousOn g s :=
  hf.uniformInducing.inducing.continuousOn_iff.symm
#align isometry.comp_continuous_on_iff Isometry.comp_continuousOn_iff

theorem comp_continuous_iff {Î³} [TopologicalSpace Î³] (hf : Isometry f) {g : Î³ â†’ Î±} :
    Continuous (f âˆ˜ g) â†” Continuous g :=
  hf.uniformInducing.inducing.continuous_iff.symm
#align isometry.comp_continuous_iff Isometry.comp_continuous_iff

end PseudoEmetricIsometry

--section
section EmetricIsometry

variable [EMetricSpace Î±] [PseudoEMetricSpace Î²] {f : Î± â†’ Î²}

/-- An isometry from an emetric space is injective -/
protected theorem injective (h : Isometry f) : Injective f :=
  h.antilipschitz.injective
#align isometry.injective Isometry.injective

/-- An isometry from an emetric space is a uniform embedding -/
protected theorem uniformEmbedding (hf : Isometry f) : UniformEmbedding f :=
  hf.antilipschitz.uniformEmbedding hf.lipschitz.uniformContinuous
#align isometry.uniform_embedding Isometry.uniformEmbedding

/-- An isometry from an emetric space is an embedding -/
protected theorem embedding (hf : Isometry f) : Embedding f :=
  hf.uniformEmbedding.embedding
#align isometry.embedding Isometry.embedding

/-- An isometry from a complete emetric space is a closed embedding -/
theorem closedEmbedding [CompleteSpace Î±] [EMetricSpace Î³] {f : Î± â†’ Î³} (hf : Isometry f) :
    ClosedEmbedding f :=
  hf.antilipschitz.closedEmbedding hf.lipschitz.uniformContinuous
#align isometry.closed_embedding Isometry.closedEmbedding

end EmetricIsometry

--section
section PseudoMetricIsometry

variable [PseudoMetricSpace Î±] [PseudoMetricSpace Î²] {f : Î± â†’ Î²}

/-- An isometry preserves the diameter in pseudometric spaces. -/
theorem diam_image (hf : Isometry f) (s : Set Î±) : Metric.diam (f '' s) = Metric.diam s := by
  rw [Metric.diam, Metric.diam, hf.ediam_image]
  -- ğŸ‰ no goals
#align isometry.diam_image Isometry.diam_image

theorem diam_range (hf : Isometry f) : Metric.diam (range f) = Metric.diam (univ : Set Î±) := by
  rw [â† image_univ]
  -- âŠ¢ Metric.diam (f '' univ) = Metric.diam univ
  exact hf.diam_image univ
  -- ğŸ‰ no goals
#align isometry.diam_range Isometry.diam_range

theorem preimage_setOf_dist (hf : Isometry f) (x : Î±) (p : â„ â†’ Prop) :
    f â»Â¹' { y | p (dist y (f x)) } = { y | p (dist y x) } := by
  ext y
  -- âŠ¢ y âˆˆ f â»Â¹' {y | p (dist y (f x))} â†” y âˆˆ {y | p (dist y x)}
  simp [hf.dist_eq]
  -- ğŸ‰ no goals
#align isometry.preimage_set_of_dist Isometry.preimage_setOf_dist

theorem preimage_closedBall (hf : Isometry f) (x : Î±) (r : â„) :
    f â»Â¹' Metric.closedBall (f x) r = Metric.closedBall x r :=
  hf.preimage_setOf_dist x (Â· â‰¤ r)
#align isometry.preimage_closed_ball Isometry.preimage_closedBall

theorem preimage_ball (hf : Isometry f) (x : Î±) (r : â„) :
    f â»Â¹' Metric.ball (f x) r = Metric.ball x r :=
  hf.preimage_setOf_dist x (Â· < r)
#align isometry.preimage_ball Isometry.preimage_ball

theorem preimage_sphere (hf : Isometry f) (x : Î±) (r : â„) :
    f â»Â¹' Metric.sphere (f x) r = Metric.sphere x r :=
  hf.preimage_setOf_dist x (Â· = r)
#align isometry.preimage_sphere Isometry.preimage_sphere

theorem mapsTo_ball (hf : Isometry f) (x : Î±) (r : â„) :
    MapsTo f (Metric.ball x r) (Metric.ball (f x) r) :=
  (hf.preimage_ball x r).ge
#align isometry.maps_to_ball Isometry.mapsTo_ball

theorem mapsTo_sphere (hf : Isometry f) (x : Î±) (r : â„) :
    MapsTo f (Metric.sphere x r) (Metric.sphere (f x) r) :=
  (hf.preimage_sphere x r).ge
#align isometry.maps_to_sphere Isometry.mapsTo_sphere

theorem mapsTo_closedBall (hf : Isometry f) (x : Î±) (r : â„) :
    MapsTo f (Metric.closedBall x r) (Metric.closedBall (f x) r) :=
  (hf.preimage_closedBall x r).ge
#align isometry.maps_to_closed_ball Isometry.mapsTo_closedBall

end PseudoMetricIsometry

-- section
end Isometry

-- namespace
/-- A uniform embedding from a uniform space to a metric space is an isometry with respect to the
induced metric space structure on the source space. -/
theorem UniformEmbedding.to_isometry {Î± Î²} [UniformSpace Î±] [MetricSpace Î²] {f : Î± â†’ Î²}
    (h : UniformEmbedding f) : (letI := h.comapMetricSpace f; Isometry f) :=
  let _ := h.comapMetricSpace f
  Isometry.of_dist_eq fun _ _ => rfl
#align uniform_embedding.to_isometry UniformEmbedding.to_isometry

/-- An embedding from a topological space to a metric space is an isometry with respect to the
induced metric space structure on the source space. -/
theorem Embedding.to_isometry {Î± Î²} [TopologicalSpace Î±] [MetricSpace Î²] {f : Î± â†’ Î²}
    (h : Embedding f) : (letI := h.comapMetricSpace f; Isometry f) :=
  let _ := h.comapMetricSpace f
  Isometry.of_dist_eq fun _ _ => rfl
#align embedding.to_isometry Embedding.to_isometry

-- such a bijection need not exist
/-- `Î±` and `Î²` are isometric if there is an isometric bijection between them. -/
-- porting note: was @[nolint has_nonempty_instance]
structure IsometryEquiv (Î± Î² : Type _) [PseudoEMetricSpace Î±] [PseudoEMetricSpace Î²] extends
  Î± â‰ƒ Î² where
  isometry_toFun : Isometry toFun
#align isometry_equiv IsometryEquiv

@[inherit_doc]
infixl:25 " â‰ƒáµ¢ " => IsometryEquiv

namespace IsometryEquiv

section PseudoEMetricSpace

variable [PseudoEMetricSpace Î±] [PseudoEMetricSpace Î²] [PseudoEMetricSpace Î³]

-- Porting note: TODO: add `IsometryEquivClass`

theorem toEquiv_injective : Injective (toEquiv : (Î± â‰ƒáµ¢ Î²) â†’ (Î± â‰ƒ Î²))
  | âŸ¨_, _âŸ©, âŸ¨_, _âŸ©, rfl => rfl
#align isometry_equiv.to_equiv_inj IsometryEquiv.toEquiv_injective

@[simp] theorem toEquiv_inj {eâ‚ eâ‚‚ : Î± â‰ƒáµ¢ Î²} : eâ‚.toEquiv = eâ‚‚.toEquiv â†” eâ‚ = eâ‚‚ :=
  toEquiv_injective.eq_iff

instance : EquivLike (Î± â‰ƒáµ¢ Î²) Î± Î² where
  coe e := e.toEquiv
  inv e := e.toEquiv.symm
  left_inv e := e.left_inv
  right_inv e := e.right_inv
  coe_injective' _ _ h _ := toEquiv_injective <| FunLike.ext' h

theorem coe_eq_toEquiv (h : Î± â‰ƒáµ¢ Î²) (a : Î±) : h a = h.toEquiv a := rfl
#align isometry_equiv.coe_eq_to_equiv IsometryEquiv.coe_eq_toEquiv

@[simp] theorem coe_toEquiv (h : Î± â‰ƒáµ¢ Î²) : â‡‘h.toEquiv = h := rfl
#align isometry_equiv.coe_to_equiv IsometryEquiv.coe_toEquiv

@[simp] theorem coe_mk (e : Î± â‰ƒ Î²) (h) : â‡‘(mk e h) = e := rfl

protected theorem isometry (h : Î± â‰ƒáµ¢ Î²) : Isometry h :=
  h.isometry_toFun
#align isometry_equiv.isometry IsometryEquiv.isometry

protected theorem bijective (h : Î± â‰ƒáµ¢ Î²) : Bijective h :=
  h.toEquiv.bijective
#align isometry_equiv.bijective IsometryEquiv.bijective

protected theorem injective (h : Î± â‰ƒáµ¢ Î²) : Injective h :=
  h.toEquiv.injective
#align isometry_equiv.injective IsometryEquiv.injective

protected theorem surjective (h : Î± â‰ƒáµ¢ Î²) : Surjective h :=
  h.toEquiv.surjective
#align isometry_equiv.surjective IsometryEquiv.surjective

protected theorem edist_eq (h : Î± â‰ƒáµ¢ Î²) (x y : Î±) : edist (h x) (h y) = edist x y :=
  h.isometry.edist_eq x y
#align isometry_equiv.edist_eq IsometryEquiv.edist_eq

protected theorem dist_eq {Î± Î² : Type*} [PseudoMetricSpace Î±] [PseudoMetricSpace Î²] (h : Î± â‰ƒáµ¢ Î²)
    (x y : Î±) : dist (h x) (h y) = dist x y :=
  h.isometry.dist_eq x y
#align isometry_equiv.dist_eq IsometryEquiv.dist_eq

protected theorem nndist_eq {Î± Î² : Type*} [PseudoMetricSpace Î±] [PseudoMetricSpace Î²] (h : Î± â‰ƒáµ¢ Î²)
    (x y : Î±) : nndist (h x) (h y) = nndist x y :=
  h.isometry.nndist_eq x y
#align isometry_equiv.nndist_eq IsometryEquiv.nndist_eq

protected theorem continuous (h : Î± â‰ƒáµ¢ Î²) : Continuous h :=
  h.isometry.continuous
#align isometry_equiv.continuous IsometryEquiv.continuous

@[simp]
theorem ediam_image (h : Î± â‰ƒáµ¢ Î²) (s : Set Î±) : EMetric.diam (h '' s) = EMetric.diam s :=
  h.isometry.ediam_image s
#align isometry_equiv.ediam_image IsometryEquiv.ediam_image

@[ext]
theorem ext â¦ƒhâ‚ hâ‚‚ : Î± â‰ƒáµ¢ Î²â¦„ (H : âˆ€ x, hâ‚ x = hâ‚‚ x) : hâ‚ = hâ‚‚ :=
  FunLike.ext _ _ H
#align isometry_equiv.ext IsometryEquiv.ext

/-- Alternative constructor for isometric bijections,
taking as input an isometry, and a right inverse. -/
def mk' {Î± : Type u} [EMetricSpace Î±] (f : Î± â†’ Î²) (g : Î² â†’ Î±) (hfg : âˆ€ x, f (g x) = x)
    (hf : Isometry f) : Î± â‰ƒáµ¢ Î² where
  toFun := f
  invFun := g
  left_inv _ := hf.injective <| hfg _
  right_inv := hfg
  isometry_toFun := hf
#align isometry_equiv.mk' IsometryEquiv.mk'

/-- The identity isometry of a space. -/
protected def refl (Î± : Type*) [PseudoEMetricSpace Î±] : Î± â‰ƒáµ¢ Î± :=
  { Equiv.refl Î± with isometry_toFun := isometry_id }
#align isometry_equiv.refl IsometryEquiv.refl

/-- The composition of two isometric isomorphisms, as an isometric isomorphism. -/
protected def trans (hâ‚ : Î± â‰ƒáµ¢ Î²) (hâ‚‚ : Î² â‰ƒáµ¢ Î³) : Î± â‰ƒáµ¢ Î³ :=
  { Equiv.trans hâ‚.toEquiv hâ‚‚.toEquiv with
    isometry_toFun := hâ‚‚.isometry_toFun.comp hâ‚.isometry_toFun }
#align isometry_equiv.trans IsometryEquiv.trans

@[simp]
theorem trans_apply (hâ‚ : Î± â‰ƒáµ¢ Î²) (hâ‚‚ : Î² â‰ƒáµ¢ Î³) (x : Î±) : hâ‚.trans hâ‚‚ x = hâ‚‚ (hâ‚ x) :=
  rfl
#align isometry_equiv.trans_apply IsometryEquiv.trans_apply

/-- The inverse of an isometric isomorphism, as an isometric isomorphism. -/
protected def symm (h : Î± â‰ƒáµ¢ Î²) : Î² â‰ƒáµ¢ Î± where
  isometry_toFun := h.isometry.right_inv h.right_inv
  toEquiv := h.toEquiv.symm
#align isometry_equiv.symm IsometryEquiv.symm

/-- See Note [custom simps projection]. We need to specify this projection explicitly in this case,
  because it is a composition of multiple projections. -/
def Simps.apply (h : Î± â‰ƒáµ¢ Î²) : Î± â†’ Î² := h
#align isometry_equiv.simps.apply IsometryEquiv.Simps.apply

/-- See Note [custom simps projection] -/
def Simps.symm_apply (h : Î± â‰ƒáµ¢ Î²) : Î² â†’ Î± :=
  h.symm
#align isometry_equiv.simps.symm_apply IsometryEquiv.Simps.symm_apply

initialize_simps_projections IsometryEquiv (toEquiv_toFun â†’ apply, toEquiv_invFun â†’ symm_apply)

@[simp]
theorem symm_symm (h : Î± â‰ƒáµ¢ Î²) : h.symm.symm = h := rfl
#align isometry_equiv.symm_symm IsometryEquiv.symm_symm

@[simp]
theorem apply_symm_apply (h : Î± â‰ƒáµ¢ Î²) (y : Î²) : h (h.symm y) = y :=
  h.toEquiv.apply_symm_apply y
#align isometry_equiv.apply_symm_apply IsometryEquiv.apply_symm_apply

@[simp]
theorem symm_apply_apply (h : Î± â‰ƒáµ¢ Î²) (x : Î±) : h.symm (h x) = x :=
  h.toEquiv.symm_apply_apply x
#align isometry_equiv.symm_apply_apply IsometryEquiv.symm_apply_apply

theorem symm_apply_eq (h : Î± â‰ƒáµ¢ Î²) {x : Î±} {y : Î²} : h.symm y = x â†” y = h x :=
  h.toEquiv.symm_apply_eq
#align isometry_equiv.symm_apply_eq IsometryEquiv.symm_apply_eq

theorem eq_symm_apply (h : Î± â‰ƒáµ¢ Î²) {x : Î±} {y : Î²} : x = h.symm y â†” h x = y :=
  h.toEquiv.eq_symm_apply
#align isometry_equiv.eq_symm_apply IsometryEquiv.eq_symm_apply

theorem symm_comp_self (h : Î± â‰ƒáµ¢ Î²) : (h.symm : Î² â†’ Î±) âˆ˜ h = id := funext h.left_inv
#align isometry_equiv.symm_comp_self IsometryEquiv.symm_comp_self

theorem self_comp_symm (h : Î± â‰ƒáµ¢ Î²) : (h : Î± â†’ Î²) âˆ˜ h.symm = id := funext h.right_inv
#align isometry_equiv.self_comp_symm IsometryEquiv.self_comp_symm

@[simp]
theorem range_eq_univ (h : Î± â‰ƒáµ¢ Î²) : range h = univ :=
  h.toEquiv.range_eq_univ
#align isometry_equiv.range_eq_univ IsometryEquiv.range_eq_univ

theorem image_symm (h : Î± â‰ƒáµ¢ Î²) : image h.symm = preimage h :=
  image_eq_preimage_of_inverse h.symm.toEquiv.left_inv h.symm.toEquiv.right_inv
#align isometry_equiv.image_symm IsometryEquiv.image_symm

theorem preimage_symm (h : Î± â‰ƒáµ¢ Î²) : preimage h.symm = image h :=
  (image_eq_preimage_of_inverse h.toEquiv.left_inv h.toEquiv.right_inv).symm
#align isometry_equiv.preimage_symm IsometryEquiv.preimage_symm

@[simp]
theorem symm_trans_apply (hâ‚ : Î± â‰ƒáµ¢ Î²) (hâ‚‚ : Î² â‰ƒáµ¢ Î³) (x : Î³) :
    (hâ‚.trans hâ‚‚).symm x = hâ‚.symm (hâ‚‚.symm x) :=
  rfl
#align isometry_equiv.symm_trans_apply IsometryEquiv.symm_trans_apply

theorem ediam_univ (h : Î± â‰ƒáµ¢ Î²) : EMetric.diam (univ : Set Î±) = EMetric.diam (univ : Set Î²) := by
  rw [â† h.range_eq_univ, h.isometry.ediam_range]
  -- ğŸ‰ no goals
#align isometry_equiv.ediam_univ IsometryEquiv.ediam_univ

@[simp]
theorem ediam_preimage (h : Î± â‰ƒáµ¢ Î²) (s : Set Î²) : EMetric.diam (h â»Â¹' s) = EMetric.diam s := by
  rw [â† image_symm, ediam_image]
  -- ğŸ‰ no goals
#align isometry_equiv.ediam_preimage IsometryEquiv.ediam_preimage

@[simp]
theorem preimage_emetric_ball (h : Î± â‰ƒáµ¢ Î²) (x : Î²) (r : â„â‰¥0âˆ) :
    h â»Â¹' EMetric.ball x r = EMetric.ball (h.symm x) r := by
  rw [â† h.isometry.preimage_emetric_ball (h.symm x) r, h.apply_symm_apply]
  -- ğŸ‰ no goals
#align isometry_equiv.preimage_emetric_ball IsometryEquiv.preimage_emetric_ball

@[simp]
theorem preimage_emetric_closedBall (h : Î± â‰ƒáµ¢ Î²) (x : Î²) (r : â„â‰¥0âˆ) :
    h â»Â¹' EMetric.closedBall x r = EMetric.closedBall (h.symm x) r := by
  rw [â† h.isometry.preimage_emetric_closedBall (h.symm x) r, h.apply_symm_apply]
  -- ğŸ‰ no goals
#align isometry_equiv.preimage_emetric_closed_ball IsometryEquiv.preimage_emetric_closedBall

@[simp]
theorem image_emetric_ball (h : Î± â‰ƒáµ¢ Î²) (x : Î±) (r : â„â‰¥0âˆ) :
    h '' EMetric.ball x r = EMetric.ball (h x) r := by
  rw [â† h.preimage_symm, h.symm.preimage_emetric_ball, symm_symm]
  -- ğŸ‰ no goals
#align isometry_equiv.image_emetric_ball IsometryEquiv.image_emetric_ball

@[simp]
theorem image_emetric_closedBall (h : Î± â‰ƒáµ¢ Î²) (x : Î±) (r : â„â‰¥0âˆ) :
    h '' EMetric.closedBall x r = EMetric.closedBall (h x) r := by
  rw [â† h.preimage_symm, h.symm.preimage_emetric_closedBall, symm_symm]
  -- ğŸ‰ no goals
#align isometry_equiv.image_emetric_closed_ball IsometryEquiv.image_emetric_closedBall

/-- The (bundled) homeomorphism associated to an isometric isomorphism. -/
@[simps toEquiv]
protected def toHomeomorph (h : Î± â‰ƒáµ¢ Î²) : Î± â‰ƒâ‚œ Î² where
  continuous_toFun := h.continuous
  continuous_invFun := h.symm.continuous
  toEquiv := h.toEquiv
#align isometry_equiv.to_homeomorph IsometryEquiv.toHomeomorph
#align isometry_equiv.to_homeomorph_to_equiv IsometryEquiv.toHomeomorph_toEquiv

@[simp]
theorem coe_toHomeomorph (h : Î± â‰ƒáµ¢ Î²) : â‡‘h.toHomeomorph = h :=
  rfl
#align isometry_equiv.coe_to_homeomorph IsometryEquiv.coe_toHomeomorph

@[simp]
theorem coe_toHomeomorph_symm (h : Î± â‰ƒáµ¢ Î²) : â‡‘h.toHomeomorph.symm = h.symm :=
  rfl
#align isometry_equiv.coe_to_homeomorph_symm IsometryEquiv.coe_toHomeomorph_symm

@[simp]
theorem comp_continuousOn_iff {Î³} [TopologicalSpace Î³] (h : Î± â‰ƒáµ¢ Î²) {f : Î³ â†’ Î±} {s : Set Î³} :
    ContinuousOn (h âˆ˜ f) s â†” ContinuousOn f s :=
  h.toHomeomorph.comp_continuousOn_iff _ _
#align isometry_equiv.comp_continuous_on_iff IsometryEquiv.comp_continuousOn_iff

@[simp]
theorem comp_continuous_iff {Î³} [TopologicalSpace Î³] (h : Î± â‰ƒáµ¢ Î²) {f : Î³ â†’ Î±} :
    Continuous (h âˆ˜ f) â†” Continuous f :=
  h.toHomeomorph.comp_continuous_iff
#align isometry_equiv.comp_continuous_iff IsometryEquiv.comp_continuous_iff

@[simp]
theorem comp_continuous_iff' {Î³} [TopologicalSpace Î³] (h : Î± â‰ƒáµ¢ Î²) {f : Î² â†’ Î³} :
    Continuous (f âˆ˜ h) â†” Continuous f :=
  h.toHomeomorph.comp_continuous_iff'
#align isometry_equiv.comp_continuous_iff' IsometryEquiv.comp_continuous_iff'

/-- The group of isometries. -/
instance : Group (Î± â‰ƒáµ¢ Î±) where
  one := IsometryEquiv.refl _
  mul eâ‚ eâ‚‚ := eâ‚‚.trans eâ‚
  inv := IsometryEquiv.symm
  mul_assoc eâ‚ eâ‚‚ eâ‚ƒ := rfl
  one_mul e := ext fun _ => rfl
  mul_one e := ext fun _ => rfl
  mul_left_inv e := ext e.symm_apply_apply

@[simp] theorem coe_one : â‡‘(1 : Î± â‰ƒáµ¢ Î±) = id := rfl
#align isometry_equiv.coe_one IsometryEquiv.coe_one

@[simp] theorem coe_mul (eâ‚ eâ‚‚ : Î± â‰ƒáµ¢ Î±) : â‡‘(eâ‚ * eâ‚‚) = eâ‚ âˆ˜ eâ‚‚ := rfl
#align isometry_equiv.coe_mul IsometryEquiv.coe_mul

theorem mul_apply (eâ‚ eâ‚‚ : Î± â‰ƒáµ¢ Î±) (x : Î±) : (eâ‚ * eâ‚‚) x = eâ‚ (eâ‚‚ x) := rfl
#align isometry_equiv.mul_apply IsometryEquiv.mul_apply

@[simp] theorem inv_apply_self (e : Î± â‰ƒáµ¢ Î±) (x : Î±) : eâ»Â¹ (e x) = x := e.symm_apply_apply x
#align isometry_equiv.inv_apply_self IsometryEquiv.inv_apply_self

@[simp] theorem apply_inv_self (e : Î± â‰ƒáµ¢ Î±) (x : Î±) : e (eâ»Â¹ x) = x := e.apply_symm_apply x
#align isometry_equiv.apply_inv_self IsometryEquiv.apply_inv_self

theorem completeSpace_iff (e : Î± â‰ƒáµ¢ Î²) : CompleteSpace Î± â†” CompleteSpace Î² := by
  simp only [completeSpace_iff_isComplete_univ, â† e.range_eq_univ, â† image_univ,
    isComplete_image_iff e.isometry.uniformInducing]
#align isometry_equiv.complete_space_iff IsometryEquiv.completeSpace_iff

protected theorem completeSpace [CompleteSpace Î²] (e : Î± â‰ƒáµ¢ Î²) : CompleteSpace Î± :=
  e.completeSpace_iff.2 â€¹_â€º
#align isometry_equiv.complete_space IsometryEquiv.completeSpace

variable (Î¹ Î±)

/-- `Equiv.funUnique` as an `IsometryEquiv`. -/
@[simps!]
def funUnique [Unique Î¹] [Fintype Î¹] : (Î¹ â†’ Î±) â‰ƒáµ¢ Î± where
  toEquiv := Equiv.funUnique Î¹ Î±
  isometry_toFun x hx := by simp [edist_pi_def, Finset.univ_unique, Finset.sup_singleton]
                            -- ğŸ‰ no goals
#align isometry_equiv.fun_unique IsometryEquiv.funUnique

/-- `piFinTwoEquiv` as an `IsometryEquiv`. -/
@[simps!]
def piFinTwo (Î± : Fin 2 â†’ Type*) [âˆ€ i, PseudoEMetricSpace (Î± i)] : (âˆ€ i, Î± i) â‰ƒáµ¢ Î± 0 Ã— Î± 1 where
  toEquiv := piFinTwoEquiv Î±
  isometry_toFun x hx := by simp [edist_pi_def, Fin.univ_succ, Prod.edist_eq]
                            -- ğŸ‰ no goals
#align isometry_equiv.pi_fin_two IsometryEquiv.piFinTwo

end PseudoEMetricSpace

section PseudoMetricSpace

variable [PseudoMetricSpace Î±] [PseudoMetricSpace Î²] (h : Î± â‰ƒáµ¢ Î²)

@[simp]
theorem diam_image (s : Set Î±) : Metric.diam (h '' s) = Metric.diam s :=
  h.isometry.diam_image s
#align isometry_equiv.diam_image IsometryEquiv.diam_image

@[simp]
theorem diam_preimage (s : Set Î²) : Metric.diam (h â»Â¹' s) = Metric.diam s := by
  rw [â† image_symm, diam_image]
  -- ğŸ‰ no goals
#align isometry_equiv.diam_preimage IsometryEquiv.diam_preimage

theorem diam_univ : Metric.diam (univ : Set Î±) = Metric.diam (univ : Set Î²) :=
  congr_arg ENNReal.toReal h.ediam_univ
#align isometry_equiv.diam_univ IsometryEquiv.diam_univ

@[simp]
theorem preimage_ball (h : Î± â‰ƒáµ¢ Î²) (x : Î²) (r : â„) :
    h â»Â¹' Metric.ball x r = Metric.ball (h.symm x) r := by
  rw [â† h.isometry.preimage_ball (h.symm x) r, h.apply_symm_apply]
  -- ğŸ‰ no goals
#align isometry_equiv.preimage_ball IsometryEquiv.preimage_ball

@[simp]
theorem preimage_sphere (h : Î± â‰ƒáµ¢ Î²) (x : Î²) (r : â„) :
    h â»Â¹' Metric.sphere x r = Metric.sphere (h.symm x) r := by
  rw [â† h.isometry.preimage_sphere (h.symm x) r, h.apply_symm_apply]
  -- ğŸ‰ no goals
#align isometry_equiv.preimage_sphere IsometryEquiv.preimage_sphere

@[simp]
theorem preimage_closedBall (h : Î± â‰ƒáµ¢ Î²) (x : Î²) (r : â„) :
    h â»Â¹' Metric.closedBall x r = Metric.closedBall (h.symm x) r := by
  rw [â† h.isometry.preimage_closedBall (h.symm x) r, h.apply_symm_apply]
  -- ğŸ‰ no goals
#align isometry_equiv.preimage_closed_ball IsometryEquiv.preimage_closedBall

@[simp]
theorem image_ball (h : Î± â‰ƒáµ¢ Î²) (x : Î±) (r : â„) : h '' Metric.ball x r = Metric.ball (h x) r := by
  rw [â† h.preimage_symm, h.symm.preimage_ball, symm_symm]
  -- ğŸ‰ no goals
#align isometry_equiv.image_ball IsometryEquiv.image_ball

@[simp]
theorem image_sphere (h : Î± â‰ƒáµ¢ Î²) (x : Î±) (r : â„) :
    h '' Metric.sphere x r = Metric.sphere (h x) r := by
  rw [â† h.preimage_symm, h.symm.preimage_sphere, symm_symm]
  -- ğŸ‰ no goals
#align isometry_equiv.image_sphere IsometryEquiv.image_sphere

@[simp]
theorem image_closedBall (h : Î± â‰ƒáµ¢ Î²) (x : Î±) (r : â„) :
    h '' Metric.closedBall x r = Metric.closedBall (h x) r := by
  rw [â† h.preimage_symm, h.symm.preimage_closedBall, symm_symm]
  -- ğŸ‰ no goals
#align isometry_equiv.image_closed_ball IsometryEquiv.image_closedBall

end PseudoMetricSpace

end IsometryEquiv

/-- An isometry induces an isometric isomorphism between the source space and the
range of the isometry. -/
@[simps! (config := { simpRhs := true }) toEquiv apply]
def Isometry.isometryEquivOnRange [EMetricSpace Î±] [PseudoEMetricSpace Î²] {f : Î± â†’ Î²}
    (h : Isometry f) : Î± â‰ƒáµ¢ range f where
  isometry_toFun := h
  toEquiv := Equiv.ofInjective f h.injective
#align isometry.isometry_equiv_on_range Isometry.isometryEquivOnRange
#align isometry.isometry_equiv_on_range_to_equiv Isometry.isometryEquivOnRange_toEquiv
#align isometry.isometry_equiv_on_range_apply Isometry.isometryEquivOnRange_apply
