/-
Copyright (c) 2015, 2017 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Robert Y. Lewis, Johannes H√∂lzl, Mario Carneiro, S√©bastien Gou√´zel
-/
import Mathlib.Tactic.Positivity
import Mathlib.Topology.Algebra.Order.Compact
import Mathlib.Topology.EMetricSpace.Basic
import Mathlib.Topology.Bornology.Constructions

#align_import topology.metric_space.basic from "leanprover-community/mathlib"@"8047de4d911cdef39c2d646165eea972f7f9f539"

/-!
# Metric spaces

This file defines metric spaces. Many definitions and theorems expected
on metric spaces are already introduced on uniform spaces and topological spaces.
For example: open and closed sets, compactness, completeness, continuity and uniform continuity

## Main definitions

* `Dist Œ±`: Endows a space `Œ±` with a function `dist a b`.
* `PseudoMetricSpace Œ±`: A space endowed with a distance function, which can
  be zero even if the two elements are non-equal.
* `Metric.ball x Œµ`: The set of all points `y` with `dist y x < Œµ`.
* `Metric.Bounded s`: Whether a subset of a `PseudoMetricSpace` is bounded.
* `MetricSpace Œ±`: A `PseudoMetricSpace` with the guarantee `dist x y = 0 ‚Üí x = y`.

Additional useful definitions:

* `nndist a b`: `dist` as a function to the non-negative reals.
* `Metric.closedBall x Œµ`: The set of all points `y` with `dist y x ‚â§ Œµ`.
* `Metric.sphere x Œµ`: The set of all points `y` with `dist y x = Œµ`.
* `ProperSpace Œ±`: A `PseudoMetricSpace` where all closed balls are compact.
* `Metric.diam s` : The `iSup` of the distances of members of `s`.
  Defined in terms of `EMetric.diam`, for better handling of the case when it should be infinite.

TODO (anyone): Add "Main results" section.

## Implementation notes

Since a lot of elementary properties don't require `eq_of_dist_eq_zero` we start setting up the
theory of `PseudoMetricSpace`, where we don't require `dist x y = 0 ‚Üí x = y` and we specialize
to `MetricSpace` at the end.

## Tags

metric, pseudo_metric, dist
-/


open Set Filter TopologicalSpace Bornology
open scoped BigOperators ENNReal NNReal Uniformity Topology

universe u v w

variable {Œ± : Type u} {Œ≤ : Type v} {X Œπ : Type*}

/-- Construct a uniform structure from a distance function and metric space axioms -/
def UniformSpace.ofDist (dist : Œ± ‚Üí Œ± ‚Üí ‚Ñù) (dist_self : ‚àÄ x : Œ±, dist x x = 0)
    (dist_comm : ‚àÄ x y : Œ±, dist x y = dist y x)
    (dist_triangle : ‚àÄ x y z : Œ±, dist x z ‚â§ dist x y + dist y z) : UniformSpace Œ± :=
  .ofFun dist dist_self dist_comm dist_triangle fun Œµ Œµ0 =>
    ‚ü®Œµ / 2, half_pos Œµ0, fun _x hx _y hy => add_halves Œµ ‚ñ∏ add_lt_add hx hy‚ü©
#align uniform_space_of_dist UniformSpace.ofDist

-- porting note: dropped the `dist_self` argument
/-- Construct a bornology from a distance function and metric space axioms. -/
@[reducible]
def Bornology.ofDist {Œ± : Type*} (dist : Œ± ‚Üí Œ± ‚Üí ‚Ñù) (dist_comm : ‚àÄ x y, dist x y = dist y x)
    (dist_triangle : ‚àÄ x y z, dist x z ‚â§ dist x y + dist y z) : Bornology Œ± :=
  Bornology.ofBounded { s : Set Œ± | ‚àÉ C, ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí ‚àÄ ‚¶Éy‚¶Ñ, y ‚àà s ‚Üí dist x y ‚â§ C }
    ‚ü®0, fun x hx y => hx.elim‚ü© (fun s ‚ü®c, hc‚ü© t h => ‚ü®c, fun x hx y hy => hc (h hx) (h hy)‚ü©)
    (fun s hs t ht => by
      rcases s.eq_empty_or_nonempty with rfl | ‚ü®x, hx‚ü©
      ¬∑ rwa [empty_union]
      rcases t.eq_empty_or_nonempty with rfl | ‚ü®y, hy‚ü©
      ¬∑ rwa [union_empty]
      rsuffices ‚ü®C, hC‚ü© : ‚àÉ C, ‚àÄ z ‚àà s ‚à™ t, dist x z ‚â§ C
      ¬∑ refine ‚ü®C + C, fun a ha b hb => (dist_triangle a x b).trans ?_‚ü©
        simpa only [dist_comm] using add_le_add (hC _ ha) (hC _ hb)
      rcases hs with ‚ü®Cs, hs‚ü©; rcases ht with ‚ü®Ct, ht‚ü©
      refine ‚ü®max Cs (dist x y + Ct), fun z hz => hz.elim
        (fun hz => (hs hx hz).trans (le_max_left _ _))
        (fun hz => (dist_triangle x y z).trans <|
          (add_le_add le_rfl (ht hy hz)).trans (le_max_right _ _))‚ü©)
    fun z => ‚ü®dist z z, forall_eq.2 <| forall_eq.2 le_rfl‚ü©
#align bornology.of_dist Bornology.ofDist‚Çì

/-- The distance function (given an ambient metric space on `Œ±`), which returns
  a nonnegative real number `dist x y` given `x y : Œ±`. -/
@[ext]
class Dist (Œ± : Type*) where
  dist : Œ± ‚Üí Œ± ‚Üí ‚Ñù
#align has_dist Dist

export Dist (dist)

-- the uniform structure and the emetric space structure are embedded in the metric space structure
-- to avoid instance diamond issues. See Note [forgetful inheritance].
/-- This is an internal lemma used inside the default of `PseudoMetricSpace.edist`. -/
private theorem dist_nonneg' {Œ±} {x y : Œ±} (dist : Œ± ‚Üí Œ± ‚Üí ‚Ñù)
    (dist_self : ‚àÄ x : Œ±, dist x x = 0) (dist_comm : ‚àÄ x y : Œ±, dist x y = dist y x)
    (dist_triangle : ‚àÄ x y z : Œ±, dist x z ‚â§ dist x y + dist y z) : 0 ‚â§ dist x y :=
  have : 0 ‚â§ 2 * dist x y :=
    calc 0 = dist x x := (dist_self _).symm
    _ ‚â§ dist x y + dist y x := dist_triangle _ _ _
    _ = 2 * dist x y := by rw [two_mul, dist_comm]
  nonneg_of_mul_nonneg_right this two_pos

#noalign pseudo_metric_space.edist_dist_tac -- porting note: todo: restore

/-- Pseudo metric and Metric spaces

A pseudo metric space is endowed with a distance for which the requirement `d(x,y)=0 ‚Üí x = y` might
not hold. A metric space is a pseudo metric space such that `d(x,y)=0 ‚Üí x = y`.
Each pseudo metric space induces a canonical `UniformSpace` and hence a canonical
`TopologicalSpace` This is enforced in the type class definition, by extending the `UniformSpace`
structure. When instantiating a `PseudoMetricSpace` structure, the uniformity fields are not
necessary, they will be filled in by default. In the same way, each (pseudo) metric space induces a
(pseudo) emetric space structure. It is included in the structure, but filled in by default.
-/
class PseudoMetricSpace (Œ± : Type u) extends Dist Œ± : Type u where
  dist_self : ‚àÄ x : Œ±, dist x x = 0
  dist_comm : ‚àÄ x y : Œ±, dist x y = dist y x
  dist_triangle : ‚àÄ x y z : Œ±, dist x z ‚â§ dist x y + dist y z
  edist : Œ± ‚Üí Œ± ‚Üí ‚Ñù‚â•0‚àû := fun x y => ENNReal.some ‚ü®dist x y, dist_nonneg' _ ‚Äπ_‚Ä∫ ‚Äπ_‚Ä∫ ‚Äπ_‚Ä∫‚ü©
  edist_dist : ‚àÄ x y : Œ±, edist x y = ENNReal.ofReal (dist x y) -- porting note: todo: add := by _
  toUniformSpace : UniformSpace Œ± := .ofDist dist dist_self dist_comm dist_triangle
  uniformity_dist : ùì§ Œ± = ‚®Ö Œµ > 0, ùìü { p : Œ± √ó Œ± | dist p.1 p.2 < Œµ } := by intros; rfl
  toBornology : Bornology Œ± := Bornology.ofDist dist dist_comm dist_triangle
  cobounded_sets : (Bornology.cobounded Œ±).sets =
    { s | ‚àÉ C : ‚Ñù, ‚àÄ x ‚àà s·∂ú, ‚àÄ y ‚àà s·∂ú, dist x y ‚â§ C } := by intros; rfl
#align pseudo_metric_space PseudoMetricSpace

/-- Two pseudo metric space structures with the same distance function coincide. -/
@[ext]
theorem PseudoMetricSpace.ext {Œ± : Type*} {m m' : PseudoMetricSpace Œ±}
    (h : m.toDist = m'.toDist) : m = m' := by
  cases' m with d _ _ _ ed hed U hU B hB
  cases' m' with d' _ _ _ ed' hed' U' hU' B' hB'
  obtain rfl : d = d' := h
  congr
  ¬∑ ext x y : 2
    rw [hed, hed']
  ¬∑ exact UniformSpace.ext (hU.trans hU'.symm)
  ¬∑ ext : 2
    rw [‚Üê Filter.mem_sets, ‚Üê Filter.mem_sets, hB, hB']
#align pseudo_metric_space.ext PseudoMetricSpace.ext

variable [PseudoMetricSpace Œ±]

attribute [instance] PseudoMetricSpace.toUniformSpace PseudoMetricSpace.toBornology

-- see Note [lower instance priority]
instance (priority := 200) PseudoMetricSpace.toEDist : EDist Œ± :=
  ‚ü®PseudoMetricSpace.edist‚ü©
#align pseudo_metric_space.to_has_edist PseudoMetricSpace.toEDist

/-- Construct a pseudo-metric space structure whose underlying topological space structure
(definitionally) agrees which a pre-existing topology which is compatible with a given distance
function. -/
def PseudoMetricSpace.ofDistTopology {Œ± : Type u} [TopologicalSpace Œ±] (dist : Œ± ‚Üí Œ± ‚Üí ‚Ñù)
    (dist_self : ‚àÄ x : Œ±, dist x x = 0) (dist_comm : ‚àÄ x y : Œ±, dist x y = dist y x)
    (dist_triangle : ‚àÄ x y z : Œ±, dist x z ‚â§ dist x y + dist y z)
    (H : ‚àÄ s : Set Œ±, IsOpen s ‚Üî ‚àÄ x ‚àà s, ‚àÉ Œµ > 0, ‚àÄ y, dist x y < Œµ ‚Üí y ‚àà s) :
    PseudoMetricSpace Œ± :=
  { dist := dist
    dist_self := dist_self
    dist_comm := dist_comm
    dist_triangle := dist_triangle
    edist_dist := fun x y => by exact ENNReal.coe_nnreal_eq _
    toUniformSpace :=
      { toCore := (UniformSpace.ofDist dist dist_self dist_comm dist_triangle).toCore
        isOpen_uniformity := fun s => (H s).trans <| forall‚ÇÇ_congr fun x _ =>
          ((UniformSpace.hasBasis_ofFun (exists_gt (0 : ‚Ñù)) dist _ _ _ _).comap
            (Prod.mk x)).mem_iff.symm.trans mem_comap_prod_mk }
    uniformity_dist := rfl
    toBornology := Bornology.ofDist dist dist_comm dist_triangle
    cobounded_sets := rfl }
#align pseudo_metric_space.of_dist_topology PseudoMetricSpace.ofDistTopology

@[simp]
theorem dist_self (x : Œ±) : dist x x = 0 :=
  PseudoMetricSpace.dist_self x
#align dist_self dist_self

theorem dist_comm (x y : Œ±) : dist x y = dist y x :=
  PseudoMetricSpace.dist_comm x y
#align dist_comm dist_comm

theorem edist_dist (x y : Œ±) : edist x y = ENNReal.ofReal (dist x y) :=
  PseudoMetricSpace.edist_dist x y
#align edist_dist edist_dist

theorem dist_triangle (x y z : Œ±) : dist x z ‚â§ dist x y + dist y z :=
  PseudoMetricSpace.dist_triangle x y z
#align dist_triangle dist_triangle

theorem dist_triangle_left (x y z : Œ±) : dist x y ‚â§ dist z x + dist z y := by
  rw [dist_comm z]; apply dist_triangle
#align dist_triangle_left dist_triangle_left

theorem dist_triangle_right (x y z : Œ±) : dist x y ‚â§ dist x z + dist y z := by
  rw [dist_comm y]; apply dist_triangle
#align dist_triangle_right dist_triangle_right

theorem dist_triangle4 (x y z w : Œ±) : dist x w ‚â§ dist x y + dist y z + dist z w :=
  calc
    dist x w ‚â§ dist x z + dist z w := dist_triangle x z w
    _ ‚â§ dist x y + dist y z + dist z w := add_le_add_right (dist_triangle x y z) _
#align dist_triangle4 dist_triangle4

theorem dist_triangle4_left (x‚ÇÅ y‚ÇÅ x‚ÇÇ y‚ÇÇ : Œ±) :
    dist x‚ÇÇ y‚ÇÇ ‚â§ dist x‚ÇÅ y‚ÇÅ + (dist x‚ÇÅ x‚ÇÇ + dist y‚ÇÅ y‚ÇÇ) := by
  rw [add_left_comm, dist_comm x‚ÇÅ, ‚Üê add_assoc]
  apply dist_triangle4
#align dist_triangle4_left dist_triangle4_left

theorem dist_triangle4_right (x‚ÇÅ y‚ÇÅ x‚ÇÇ y‚ÇÇ : Œ±) :
    dist x‚ÇÅ y‚ÇÅ ‚â§ dist x‚ÇÅ x‚ÇÇ + dist y‚ÇÅ y‚ÇÇ + dist x‚ÇÇ y‚ÇÇ := by
  rw [add_right_comm, dist_comm y‚ÇÅ]
  apply dist_triangle4
#align dist_triangle4_right dist_triangle4_right

/-- The triangle (polygon) inequality for sequences of points; `Finset.Ico` version. -/
theorem dist_le_Ico_sum_dist (f : ‚Ñï ‚Üí Œ±) {m n} (h : m ‚â§ n) :
    dist (f m) (f n) ‚â§ ‚àë i in Finset.Ico m n, dist (f i) (f (i + 1)) := by
  induction n, h using Nat.le_induction with
  | base => rw [Finset.Ico_self, Finset.sum_empty, dist_self]
  | succ n hle ihn =>
    calc
      dist (f m) (f (n + 1)) ‚â§ dist (f m) (f n) + dist (f n) (f (n + 1)) := dist_triangle _ _ _
      _ ‚â§ (‚àë i in Finset.Ico m n, _) + _ := add_le_add ihn le_rfl
      _ = ‚àë i in Finset.Ico m (n + 1), _ := by
      { rw [Nat.Ico_succ_right_eq_insert_Ico hle, Finset.sum_insert, add_comm]; simp }
#align dist_le_Ico_sum_dist dist_le_Ico_sum_dist

/-- The triangle (polygon) inequality for sequences of points; `Finset.range` version. -/
theorem dist_le_range_sum_dist (f : ‚Ñï ‚Üí Œ±) (n : ‚Ñï) :
    dist (f 0) (f n) ‚â§ ‚àë i in Finset.range n, dist (f i) (f (i + 1)) :=
  Nat.Ico_zero_eq_range ‚ñ∏ dist_le_Ico_sum_dist f (Nat.zero_le n)
#align dist_le_range_sum_dist dist_le_range_sum_dist

/-- A version of `dist_le_Ico_sum_dist` with each intermediate distance replaced
with an upper estimate. -/
theorem dist_le_Ico_sum_of_dist_le {f : ‚Ñï ‚Üí Œ±} {m n} (hmn : m ‚â§ n) {d : ‚Ñï ‚Üí ‚Ñù}
    (hd : ‚àÄ {k}, m ‚â§ k ‚Üí k < n ‚Üí dist (f k) (f (k + 1)) ‚â§ d k) :
    dist (f m) (f n) ‚â§ ‚àë i in Finset.Ico m n, d i :=
  le_trans (dist_le_Ico_sum_dist f hmn) <|
    Finset.sum_le_sum fun _k hk => hd (Finset.mem_Ico.1 hk).1 (Finset.mem_Ico.1 hk).2
#align dist_le_Ico_sum_of_dist_le dist_le_Ico_sum_of_dist_le

/-- A version of `dist_le_range_sum_dist` with each intermediate distance replaced
with an upper estimate. -/
theorem dist_le_range_sum_of_dist_le {f : ‚Ñï ‚Üí Œ±} (n : ‚Ñï) {d : ‚Ñï ‚Üí ‚Ñù}
    (hd : ‚àÄ {k}, k < n ‚Üí dist (f k) (f (k + 1)) ‚â§ d k) :
    dist (f 0) (f n) ‚â§ ‚àë i in Finset.range n, d i :=
  Nat.Ico_zero_eq_range ‚ñ∏ dist_le_Ico_sum_of_dist_le (zero_le n) fun _ => hd
#align dist_le_range_sum_of_dist_le dist_le_range_sum_of_dist_le

theorem swap_dist : Function.swap (@dist Œ± _) = dist := by funext x y; exact dist_comm _ _
#align swap_dist swap_dist

theorem abs_dist_sub_le (x y z : Œ±) : |dist x z - dist y z| ‚â§ dist x y :=
  abs_sub_le_iff.2
    ‚ü®sub_le_iff_le_add.2 (dist_triangle _ _ _), sub_le_iff_le_add.2 (dist_triangle_left _ _ _)‚ü©
#align abs_dist_sub_le abs_dist_sub_le

theorem dist_nonneg {x y : Œ±} : 0 ‚â§ dist x y :=
  dist_nonneg' dist dist_self dist_comm dist_triangle
#align dist_nonneg dist_nonneg

namespace Mathlib.Meta.Positivity

open Lean Meta Qq Function

/-- Extension for the `positivity` tactic: distances are nonnegative. -/
@[positivity Dist.dist _ _]
def evalDist : PositivityExt where eval {_ _} _zŒ± _pŒ± e := do
  let .app (.app _ a) b ‚Üê whnfR e | throwError "not dist"
  let p ‚Üê mkAppOptM ``dist_nonneg #[none, none, a, b]
  pure (.nonnegative p)

end Mathlib.Meta.Positivity

example {x y : Œ±} : 0 ‚â§ dist x y := by positivity

@[simp] theorem abs_dist {a b : Œ±} : |dist a b| = dist a b := abs_of_nonneg dist_nonneg
#align abs_dist abs_dist

/-- A version of `Dist` that takes value in `‚Ñù‚â•0`. -/
class NNDist (Œ± : Type*) where
  nndist : Œ± ‚Üí Œ± ‚Üí ‚Ñù‚â•0
#align has_nndist NNDist

export NNDist (nndist)

-- see Note [lower instance priority]
/-- Distance as a nonnegative real number. -/
instance (priority := 100) PseudoMetricSpace.toNNDist : NNDist Œ± :=
  ‚ü®fun a b => ‚ü®dist a b, dist_nonneg‚ü©‚ü©
#align pseudo_metric_space.to_has_nndist PseudoMetricSpace.toNNDist

/-- Express `dist` in terms of `nndist`-/
theorem dist_nndist (x y : Œ±) : dist x y = nndist x y := rfl
#align dist_nndist dist_nndist

@[simp, norm_cast]
theorem coe_nndist (x y : Œ±) : ‚Üë(nndist x y) = dist x y := rfl
#align coe_nndist coe_nndist

/-- Express `edist` in terms of `nndist`-/
theorem edist_nndist (x y : Œ±) : edist x y = nndist x y := by
  rw [edist_dist, dist_nndist, ENNReal.ofReal_coe_nnreal]
#align edist_nndist edist_nndist

/-- Express `nndist` in terms of `edist`-/
theorem nndist_edist (x y : Œ±) : nndist x y = (edist x y).toNNReal := by
  simp [edist_nndist]
#align nndist_edist nndist_edist

@[simp, norm_cast]
theorem coe_nnreal_ennreal_nndist (x y : Œ±) : ‚Üë(nndist x y) = edist x y :=
  (edist_nndist x y).symm
#align coe_nnreal_ennreal_nndist coe_nnreal_ennreal_nndist

@[simp, norm_cast]
theorem edist_lt_coe {x y : Œ±} {c : ‚Ñù‚â•0} : edist x y < c ‚Üî nndist x y < c := by
  rw [edist_nndist, ENNReal.coe_lt_coe]
#align edist_lt_coe edist_lt_coe

@[simp, norm_cast]
theorem edist_le_coe {x y : Œ±} {c : ‚Ñù‚â•0} : edist x y ‚â§ c ‚Üî nndist x y ‚â§ c := by
  rw [edist_nndist, ENNReal.coe_le_coe]
#align edist_le_coe edist_le_coe

/-- In a pseudometric space, the extended distance is always finite-/
theorem edist_lt_top {Œ± : Type*} [PseudoMetricSpace Œ±] (x y : Œ±) : edist x y < ‚ä§ :=
  (edist_dist x y).symm ‚ñ∏ ENNReal.ofReal_lt_top
#align edist_lt_top edist_lt_top

/-- In a pseudometric space, the extended distance is always finite-/
theorem edist_ne_top (x y : Œ±) : edist x y ‚â† ‚ä§ :=
  (edist_lt_top x y).ne
#align edist_ne_top edist_ne_top

/-- `nndist x x` vanishes-/
@[simp]
theorem nndist_self (a : Œ±) : nndist a a = 0 :=
  (NNReal.coe_eq_zero _).1 (dist_self a)
#align nndist_self nndist_self

-- porting note: `dist_nndist` and `coe_nndist` moved up

@[simp, norm_cast]
theorem dist_lt_coe {x y : Œ±} {c : ‚Ñù‚â•0} : dist x y < c ‚Üî nndist x y < c :=
  Iff.rfl
#align dist_lt_coe dist_lt_coe

@[simp, norm_cast]
theorem dist_le_coe {x y : Œ±} {c : ‚Ñù‚â•0} : dist x y ‚â§ c ‚Üî nndist x y ‚â§ c :=
  Iff.rfl
#align dist_le_coe dist_le_coe

@[simp]
theorem edist_lt_ofReal {x y : Œ±} {r : ‚Ñù} : edist x y < ENNReal.ofReal r ‚Üî dist x y < r := by
  rw [edist_dist, ENNReal.ofReal_lt_ofReal_iff_of_nonneg dist_nonneg]
#align edist_lt_of_real edist_lt_ofReal

@[simp]
theorem edist_le_ofReal {x y : Œ±} {r : ‚Ñù} (hr : 0 ‚â§ r) :
    edist x y ‚â§ ENNReal.ofReal r ‚Üî dist x y ‚â§ r := by
  rw [edist_dist, ENNReal.ofReal_le_ofReal_iff hr]
#align edist_le_of_real edist_le_ofReal

/-- Express `nndist` in terms of `dist`-/
theorem nndist_dist (x y : Œ±) : nndist x y = Real.toNNReal (dist x y) := by
  rw [dist_nndist, Real.toNNReal_coe]
#align nndist_dist nndist_dist

theorem nndist_comm (x y : Œ±) : nndist x y = nndist y x := NNReal.eq <| dist_comm x y
#align nndist_comm nndist_comm

/-- Triangle inequality for the nonnegative distance-/
theorem nndist_triangle (x y z : Œ±) : nndist x z ‚â§ nndist x y + nndist y z :=
  dist_triangle _ _ _
#align nndist_triangle nndist_triangle

theorem nndist_triangle_left (x y z : Œ±) : nndist x y ‚â§ nndist z x + nndist z y :=
  dist_triangle_left _ _ _
#align nndist_triangle_left nndist_triangle_left

theorem nndist_triangle_right (x y z : Œ±) : nndist x y ‚â§ nndist x z + nndist y z :=
  dist_triangle_right _ _ _
#align nndist_triangle_right nndist_triangle_right

/-- Express `dist` in terms of `edist`-/
theorem dist_edist (x y : Œ±) : dist x y = (edist x y).toReal := by
  rw [edist_dist, ENNReal.toReal_ofReal dist_nonneg]
#align dist_edist dist_edist

namespace Metric

-- instantiate pseudometric space as a topology
variable {x y z : Œ±} {Œ¥ Œµ Œµ‚ÇÅ Œµ‚ÇÇ : ‚Ñù} {s : Set Œ±}

/-- `ball x Œµ` is the set of all points `y` with `dist y x < Œµ` -/
def ball (x : Œ±) (Œµ : ‚Ñù) : Set Œ± :=
  { y | dist y x < Œµ }
#align metric.ball Metric.ball

@[simp]
theorem mem_ball : y ‚àà ball x Œµ ‚Üî dist y x < Œµ :=
  Iff.rfl
#align metric.mem_ball Metric.mem_ball

theorem mem_ball' : y ‚àà ball x Œµ ‚Üî dist x y < Œµ := by rw [dist_comm, mem_ball]
#align metric.mem_ball' Metric.mem_ball'

theorem pos_of_mem_ball (hy : y ‚àà ball x Œµ) : 0 < Œµ :=
  dist_nonneg.trans_lt hy
#align metric.pos_of_mem_ball Metric.pos_of_mem_ball

theorem mem_ball_self (h : 0 < Œµ) : x ‚àà ball x Œµ := by
  rwa [mem_ball, dist_self]
#align metric.mem_ball_self Metric.mem_ball_self

@[simp]
theorem nonempty_ball : (ball x Œµ).Nonempty ‚Üî 0 < Œµ :=
  ‚ü®fun ‚ü®_x, hx‚ü© => pos_of_mem_ball hx, fun h => ‚ü®x, mem_ball_self h‚ü©‚ü©
#align metric.nonempty_ball Metric.nonempty_ball

@[simp]
theorem ball_eq_empty : ball x Œµ = ‚àÖ ‚Üî Œµ ‚â§ 0 := by
  rw [‚Üê not_nonempty_iff_eq_empty, nonempty_ball, not_lt]
#align metric.ball_eq_empty Metric.ball_eq_empty

@[simp]
theorem ball_zero : ball x 0 = ‚àÖ := by rw [ball_eq_empty]
#align metric.ball_zero Metric.ball_zero

/-- If a point belongs to an open ball, then there is a strictly smaller radius whose ball also
contains it.

See also `exists_lt_subset_ball`. -/
theorem exists_lt_mem_ball_of_mem_ball (h : x ‚àà ball y Œµ) : ‚àÉ Œµ' < Œµ, x ‚àà ball y Œµ' := by
  simp only [mem_ball] at h ‚ä¢
  exact ‚ü®(dist x y + Œµ) / 2, by linarith, by linarith‚ü©
#align metric.exists_lt_mem_ball_of_mem_ball Metric.exists_lt_mem_ball_of_mem_ball

theorem ball_eq_ball (Œµ : ‚Ñù) (x : Œ±) :
    UniformSpace.ball x { p | dist p.2 p.1 < Œµ } = Metric.ball x Œµ :=
  rfl
#align metric.ball_eq_ball Metric.ball_eq_ball

theorem ball_eq_ball' (Œµ : ‚Ñù) (x : Œ±) :
    UniformSpace.ball x { p | dist p.1 p.2 < Œµ } = Metric.ball x Œµ := by
  ext
  simp [dist_comm, UniformSpace.ball]
#align metric.ball_eq_ball' Metric.ball_eq_ball'

@[simp]
theorem iUnion_ball_nat (x : Œ±) : ‚ãÉ n : ‚Ñï, ball x n = univ :=
  iUnion_eq_univ_iff.2 fun y => exists_nat_gt (dist y x)
#align metric.Union_ball_nat Metric.iUnion_ball_nat

@[simp]
theorem iUnion_ball_nat_succ (x : Œ±) : ‚ãÉ n : ‚Ñï, ball x (n + 1) = univ :=
  iUnion_eq_univ_iff.2 fun y => (exists_nat_gt (dist y x)).imp fun _ h => h.trans (lt_add_one _)
#align metric.Union_ball_nat_succ Metric.iUnion_ball_nat_succ

/-- `closedBall x Œµ` is the set of all points `y` with `dist y x ‚â§ Œµ` -/
def closedBall (x : Œ±) (Œµ : ‚Ñù) :=
  { y | dist y x ‚â§ Œµ }
#align metric.closed_ball Metric.closedBall

@[simp] theorem mem_closedBall : y ‚àà closedBall x Œµ ‚Üî dist y x ‚â§ Œµ := Iff.rfl
#align metric.mem_closed_ball Metric.mem_closedBall

theorem mem_closedBall' : y ‚àà closedBall x Œµ ‚Üî dist x y ‚â§ Œµ := by rw [dist_comm, mem_closedBall]
#align metric.mem_closed_ball' Metric.mem_closedBall'

/-- `sphere x Œµ` is the set of all points `y` with `dist y x = Œµ` -/
def sphere (x : Œ±) (Œµ : ‚Ñù) := { y | dist y x = Œµ }
#align metric.sphere Metric.sphere

@[simp] theorem mem_sphere : y ‚àà sphere x Œµ ‚Üî dist y x = Œµ := Iff.rfl
#align metric.mem_sphere Metric.mem_sphere

theorem mem_sphere' : y ‚àà sphere x Œµ ‚Üî dist x y = Œµ := by rw [dist_comm, mem_sphere]
#align metric.mem_sphere' Metric.mem_sphere'

theorem ne_of_mem_sphere (h : y ‚àà sphere x Œµ) (hŒµ : Œµ ‚â† 0) : y ‚â† x :=
  ne_of_mem_of_not_mem h <| by simpa using hŒµ.symm
#align metric.ne_of_mem_sphere Metric.ne_of_mem_sphere

theorem nonneg_of_mem_sphere (hy : y ‚àà sphere x Œµ) : 0 ‚â§ Œµ :=
  dist_nonneg.trans_eq hy
#align metric.nonneg_of_mem_sphere Metric.nonneg_of_mem_sphere

@[simp]
theorem sphere_eq_empty_of_neg (hŒµ : Œµ < 0) : sphere x Œµ = ‚àÖ :=
  Set.eq_empty_iff_forall_not_mem.mpr fun _y hy => (nonneg_of_mem_sphere hy).not_lt hŒµ
#align metric.sphere_eq_empty_of_neg Metric.sphere_eq_empty_of_neg

theorem sphere_eq_empty_of_subsingleton [Subsingleton Œ±] (hŒµ : Œµ ‚â† 0) : sphere x Œµ = ‚àÖ :=
  Set.eq_empty_iff_forall_not_mem.mpr fun _ h => ne_of_mem_sphere h hŒµ (Subsingleton.elim _ _)
#align metric.sphere_eq_empty_of_subsingleton Metric.sphere_eq_empty_of_subsingleton

theorem sphere_isEmpty_of_subsingleton [Subsingleton Œ±] (hŒµ : Œµ ‚â† 0) : IsEmpty (sphere x Œµ) := by
  rw [sphere_eq_empty_of_subsingleton hŒµ]; infer_instance
#align metric.sphere_is_empty_of_subsingleton Metric.sphere_isEmpty_of_subsingleton

theorem mem_closedBall_self (h : 0 ‚â§ Œµ) : x ‚àà closedBall x Œµ := by
  rwa [mem_closedBall, dist_self]
#align metric.mem_closed_ball_self Metric.mem_closedBall_self

@[simp]
theorem nonempty_closedBall : (closedBall x Œµ).Nonempty ‚Üî 0 ‚â§ Œµ :=
  ‚ü®fun ‚ü®_x, hx‚ü© => dist_nonneg.trans hx, fun h => ‚ü®x, mem_closedBall_self h‚ü©‚ü©
#align metric.nonempty_closed_ball Metric.nonempty_closedBall

@[simp]
theorem closedBall_eq_empty : closedBall x Œµ = ‚àÖ ‚Üî Œµ < 0 := by
  rw [‚Üê not_nonempty_iff_eq_empty, nonempty_closedBall, not_le]
#align metric.closed_ball_eq_empty Metric.closedBall_eq_empty

/-- Closed balls and spheres coincide when the radius is non-positive -/
theorem closedBall_eq_sphere_of_nonpos (hŒµ : Œµ ‚â§ 0) : closedBall x Œµ = sphere x Œµ :=
  Set.ext fun _ => (hŒµ.trans dist_nonneg).le_iff_eq
#align metric.closed_ball_eq_sphere_of_nonpos Metric.closedBall_eq_sphere_of_nonpos

theorem ball_subset_closedBall : ball x Œµ ‚äÜ closedBall x Œµ := fun _y hy =>
  mem_closedBall.2 (le_of_lt hy)
#align metric.ball_subset_closed_ball Metric.ball_subset_closedBall

theorem sphere_subset_closedBall : sphere x Œµ ‚äÜ closedBall x Œµ := fun _ => le_of_eq
#align metric.sphere_subset_closed_ball Metric.sphere_subset_closedBall

theorem closedBall_disjoint_ball (h : Œ¥ + Œµ ‚â§ dist x y) : Disjoint (closedBall x Œ¥) (ball y Œµ) :=
  Set.disjoint_left.mpr fun _a ha1 ha2 =>
    (h.trans <| dist_triangle_left _ _ _).not_lt <| add_lt_add_of_le_of_lt ha1 ha2
#align metric.closed_ball_disjoint_ball Metric.closedBall_disjoint_ball

theorem ball_disjoint_closedBall (h : Œ¥ + Œµ ‚â§ dist x y) : Disjoint (ball x Œ¥) (closedBall y Œµ) :=
  (closedBall_disjoint_ball <| by rwa [add_comm, dist_comm]).symm
#align metric.ball_disjoint_closed_ball Metric.ball_disjoint_closedBall

theorem ball_disjoint_ball (h : Œ¥ + Œµ ‚â§ dist x y) : Disjoint (ball x Œ¥) (ball y Œµ) :=
  (closedBall_disjoint_ball h).mono_left ball_subset_closedBall
#align metric.ball_disjoint_ball Metric.ball_disjoint_ball

theorem closedBall_disjoint_closedBall (h : Œ¥ + Œµ < dist x y) :
    Disjoint (closedBall x Œ¥) (closedBall y Œµ) :=
  Set.disjoint_left.mpr fun _a ha1 ha2 =>
    h.not_le <| (dist_triangle_left _ _ _).trans <| add_le_add ha1 ha2
#align metric.closed_ball_disjoint_closed_ball Metric.closedBall_disjoint_closedBall

theorem sphere_disjoint_ball : Disjoint (sphere x Œµ) (ball x Œµ) :=
  Set.disjoint_left.mpr fun _y hy‚ÇÅ hy‚ÇÇ => absurd hy‚ÇÅ <| ne_of_lt hy‚ÇÇ
#align metric.sphere_disjoint_ball Metric.sphere_disjoint_ball

@[simp]
theorem ball_union_sphere : ball x Œµ ‚à™ sphere x Œµ = closedBall x Œµ :=
  Set.ext fun _y => (@le_iff_lt_or_eq ‚Ñù _ _ _).symm
#align metric.ball_union_sphere Metric.ball_union_sphere

@[simp]
theorem sphere_union_ball : sphere x Œµ ‚à™ ball x Œµ = closedBall x Œµ := by
  rw [union_comm, ball_union_sphere]
#align metric.sphere_union_ball Metric.sphere_union_ball

@[simp]
theorem closedBall_diff_sphere : closedBall x Œµ \ sphere x Œµ = ball x Œµ := by
  rw [‚Üê ball_union_sphere, Set.union_diff_cancel_right sphere_disjoint_ball.symm.le_bot]
#align metric.closed_ball_diff_sphere Metric.closedBall_diff_sphere

@[simp]
theorem closedBall_diff_ball : closedBall x Œµ \ ball x Œµ = sphere x Œµ := by
  rw [‚Üê ball_union_sphere, Set.union_diff_cancel_left sphere_disjoint_ball.symm.le_bot]
#align metric.closed_ball_diff_ball Metric.closedBall_diff_ball

theorem mem_ball_comm : x ‚àà ball y Œµ ‚Üî y ‚àà ball x Œµ := by rw [mem_ball', mem_ball]
#align metric.mem_ball_comm Metric.mem_ball_comm

theorem mem_closedBall_comm : x ‚àà closedBall y Œµ ‚Üî y ‚àà closedBall x Œµ := by
  rw [mem_closedBall', mem_closedBall]
#align metric.mem_closed_ball_comm Metric.mem_closedBall_comm

theorem mem_sphere_comm : x ‚àà sphere y Œµ ‚Üî y ‚àà sphere x Œµ := by rw [mem_sphere', mem_sphere]
#align metric.mem_sphere_comm Metric.mem_sphere_comm

theorem ball_subset_ball (h : Œµ‚ÇÅ ‚â§ Œµ‚ÇÇ) : ball x Œµ‚ÇÅ ‚äÜ ball x Œµ‚ÇÇ := fun _y yx =>
  lt_of_lt_of_le (mem_ball.1 yx) h
#align metric.ball_subset_ball Metric.ball_subset_ball

theorem closedBall_eq_bInter_ball : closedBall x Œµ = ‚ãÇ Œ¥ > Œµ, ball x Œ¥ := by
  ext y; rw [mem_closedBall, ‚Üê forall_lt_iff_le', mem_iInter‚ÇÇ]; rfl
#align metric.closed_ball_eq_bInter_ball Metric.closedBall_eq_bInter_ball

theorem ball_subset_ball' (h : Œµ‚ÇÅ + dist x y ‚â§ Œµ‚ÇÇ) : ball x Œµ‚ÇÅ ‚äÜ ball y Œµ‚ÇÇ := fun z hz =>
  calc
    dist z y ‚â§ dist z x + dist x y := dist_triangle _ _ _
    _ < Œµ‚ÇÅ + dist x y := add_lt_add_right (mem_ball.1 hz) _
    _ ‚â§ Œµ‚ÇÇ := h
#align metric.ball_subset_ball' Metric.ball_subset_ball'

theorem closedBall_subset_closedBall (h : Œµ‚ÇÅ ‚â§ Œµ‚ÇÇ) : closedBall x Œµ‚ÇÅ ‚äÜ closedBall x Œµ‚ÇÇ :=
  fun _y (yx : _ ‚â§ Œµ‚ÇÅ) => le_trans yx h
#align metric.closed_ball_subset_closed_ball Metric.closedBall_subset_closedBall

theorem closedBall_subset_closedBall' (h : Œµ‚ÇÅ + dist x y ‚â§ Œµ‚ÇÇ) :
    closedBall x Œµ‚ÇÅ ‚äÜ closedBall y Œµ‚ÇÇ := fun z hz =>
  calc
    dist z y ‚â§ dist z x + dist x y := dist_triangle _ _ _
    _ ‚â§ Œµ‚ÇÅ + dist x y := add_le_add_right (mem_closedBall.1 hz) _
    _ ‚â§ Œµ‚ÇÇ := h
#align metric.closed_ball_subset_closed_ball' Metric.closedBall_subset_closedBall'

theorem closedBall_subset_ball (h : Œµ‚ÇÅ < Œµ‚ÇÇ) : closedBall x Œµ‚ÇÅ ‚äÜ ball x Œµ‚ÇÇ :=
  fun y (yh : dist y x ‚â§ Œµ‚ÇÅ) => lt_of_le_of_lt yh h
#align metric.closed_ball_subset_ball Metric.closedBall_subset_ball

theorem closedBall_subset_ball' (h : Œµ‚ÇÅ + dist x y < Œµ‚ÇÇ) :
    closedBall x Œµ‚ÇÅ ‚äÜ ball y Œµ‚ÇÇ := fun z hz =>
  calc
    dist z y ‚â§ dist z x + dist x y := dist_triangle _ _ _
    _ ‚â§ Œµ‚ÇÅ + dist x y := add_le_add_right (mem_closedBall.1 hz) _
    _ < Œµ‚ÇÇ := h
#align metric.closed_ball_subset_ball' Metric.closedBall_subset_ball'

theorem dist_le_add_of_nonempty_closedBall_inter_closedBall
    (h : (closedBall x Œµ‚ÇÅ ‚à© closedBall y Œµ‚ÇÇ).Nonempty) : dist x y ‚â§ Œµ‚ÇÅ + Œµ‚ÇÇ :=
  let ‚ü®z, hz‚ü© := h
  calc
    dist x y ‚â§ dist z x + dist z y := dist_triangle_left _ _ _
    _ ‚â§ Œµ‚ÇÅ + Œµ‚ÇÇ := add_le_add hz.1 hz.2
#align metric.dist_le_add_of_nonempty_closed_ball_inter_closed_ball Metric.dist_le_add_of_nonempty_closedBall_inter_closedBall

theorem dist_lt_add_of_nonempty_closedBall_inter_ball (h : (closedBall x Œµ‚ÇÅ ‚à© ball y Œµ‚ÇÇ).Nonempty) :
    dist x y < Œµ‚ÇÅ + Œµ‚ÇÇ :=
  let ‚ü®z, hz‚ü© := h
  calc
    dist x y ‚â§ dist z x + dist z y := dist_triangle_left _ _ _
    _ < Œµ‚ÇÅ + Œµ‚ÇÇ := add_lt_add_of_le_of_lt hz.1 hz.2
#align metric.dist_lt_add_of_nonempty_closed_ball_inter_ball Metric.dist_lt_add_of_nonempty_closedBall_inter_ball

theorem dist_lt_add_of_nonempty_ball_inter_closedBall (h : (ball x Œµ‚ÇÅ ‚à© closedBall y Œµ‚ÇÇ).Nonempty) :
    dist x y < Œµ‚ÇÅ + Œµ‚ÇÇ := by
  rw [inter_comm] at h
  rw [add_comm, dist_comm]
  exact dist_lt_add_of_nonempty_closedBall_inter_ball h
#align metric.dist_lt_add_of_nonempty_ball_inter_closed_ball Metric.dist_lt_add_of_nonempty_ball_inter_closedBall

theorem dist_lt_add_of_nonempty_ball_inter_ball (h : (ball x Œµ‚ÇÅ ‚à© ball y Œµ‚ÇÇ).Nonempty) :
    dist x y < Œµ‚ÇÅ + Œµ‚ÇÇ :=
  dist_lt_add_of_nonempty_closedBall_inter_ball <|
    h.mono (inter_subset_inter ball_subset_closedBall Subset.rfl)
#align metric.dist_lt_add_of_nonempty_ball_inter_ball Metric.dist_lt_add_of_nonempty_ball_inter_ball

@[simp]
theorem iUnion_closedBall_nat (x : Œ±) : ‚ãÉ n : ‚Ñï, closedBall x n = univ :=
  iUnion_eq_univ_iff.2 fun y => exists_nat_ge (dist y x)
#align metric.Union_closed_ball_nat Metric.iUnion_closedBall_nat

theorem iUnion_inter_closedBall_nat (s : Set Œ±) (x : Œ±) : ‚ãÉ n : ‚Ñï, s ‚à© closedBall x n = s := by
  rw [‚Üê inter_iUnion, iUnion_closedBall_nat, inter_univ]
#align metric.Union_inter_closed_ball_nat Metric.iUnion_inter_closedBall_nat

theorem ball_subset (h : dist x y ‚â§ Œµ‚ÇÇ - Œµ‚ÇÅ) : ball x Œµ‚ÇÅ ‚äÜ ball y Œµ‚ÇÇ := fun z zx => by
  rw [‚Üê add_sub_cancel'_right Œµ‚ÇÅ Œµ‚ÇÇ]
  exact lt_of_le_of_lt (dist_triangle z x y) (add_lt_add_of_lt_of_le zx h)
#align metric.ball_subset Metric.ball_subset

theorem ball_half_subset (y) (h : y ‚àà ball x (Œµ / 2)) : ball y (Œµ / 2) ‚äÜ ball x Œµ :=
  ball_subset <| by rw [sub_self_div_two]; exact le_of_lt h
#align metric.ball_half_subset Metric.ball_half_subset

theorem exists_ball_subset_ball (h : y ‚àà ball x Œµ) : ‚àÉ Œµ' > 0, ball y Œµ' ‚äÜ ball x Œµ :=
  ‚ü®_, sub_pos.2 h, ball_subset <| by rw [sub_sub_self]‚ü©
#align metric.exists_ball_subset_ball Metric.exists_ball_subset_ball

/-- If a property holds for all points in closed balls of arbitrarily large radii, then it holds for
all points. -/
theorem forall_of_forall_mem_closedBall (p : Œ± ‚Üí Prop) (x : Œ±)
    (H : ‚àÉ·∂† R : ‚Ñù in atTop, ‚àÄ y ‚àà closedBall x R, p y) (y : Œ±) : p y := by
  obtain ‚ü®R, hR, h‚ü© : ‚àÉ R ‚â• dist y x, ‚àÄ z : Œ±, z ‚àà closedBall x R ‚Üí p z :=
    frequently_iff.1 H (Ici_mem_atTop (dist y x))
  exact h _ hR
#align metric.forall_of_forall_mem_closed_ball Metric.forall_of_forall_mem_closedBall

/-- If a property holds for all points in balls of arbitrarily large radii, then it holds for all
points. -/
theorem forall_of_forall_mem_ball (p : Œ± ‚Üí Prop) (x : Œ±)
    (H : ‚àÉ·∂† R : ‚Ñù in atTop, ‚àÄ y ‚àà ball x R, p y) (y : Œ±) : p y := by
  obtain ‚ü®R, hR, h‚ü© : ‚àÉ R > dist y x, ‚àÄ z : Œ±, z ‚àà ball x R ‚Üí p z :=
    frequently_iff.1 H (Ioi_mem_atTop (dist y x))
  exact h _ hR
#align metric.forall_of_forall_mem_ball Metric.forall_of_forall_mem_ball

theorem isBounded_iff {s : Set Œ±} :
    IsBounded s ‚Üî ‚àÉ C : ‚Ñù, ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí ‚àÄ ‚¶Éy‚¶Ñ, y ‚àà s ‚Üí dist x y ‚â§ C := by
  rw [isBounded_def, ‚Üê Filter.mem_sets, @PseudoMetricSpace.cobounded_sets Œ±, mem_setOf_eq,
    compl_compl]
#align metric.is_bounded_iff Metric.isBounded_iff

theorem isBounded_iff_eventually {s : Set Œ±} :
    IsBounded s ‚Üî ‚àÄ·∂† C in atTop, ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí ‚àÄ ‚¶Éy‚¶Ñ, y ‚àà s ‚Üí dist x y ‚â§ C :=
  isBounded_iff.trans
    ‚ü®fun ‚ü®C, h‚ü© => eventually_atTop.2 ‚ü®C, fun _C' hC' _x hx _y hy => (h hx hy).trans hC'‚ü©,
      Eventually.exists‚ü©
#align metric.is_bounded_iff_eventually Metric.isBounded_iff_eventually

theorem isBounded_iff_exists_ge {s : Set Œ±} (c : ‚Ñù) :
    IsBounded s ‚Üî ‚àÉ C, c ‚â§ C ‚àß ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí ‚àÄ ‚¶Éy‚¶Ñ, y ‚àà s ‚Üí dist x y ‚â§ C :=
  ‚ü®fun h => ((eventually_ge_atTop c).and (isBounded_iff_eventually.1 h)).exists, fun h =>
    isBounded_iff.2 <| h.imp fun _ => And.right‚ü©
#align metric.is_bounded_iff_exists_ge Metric.isBounded_iff_exists_ge

theorem isBounded_iff_nndist {s : Set Œ±} :
    IsBounded s ‚Üî ‚àÉ C : ‚Ñù‚â•0, ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí ‚àÄ ‚¶Éy‚¶Ñ, y ‚àà s ‚Üí nndist x y ‚â§ C := by
  simp only [isBounded_iff_exists_ge 0, NNReal.exists, ‚Üê NNReal.coe_le_coe, ‚Üê dist_nndist,
    NNReal.coe_mk, exists_prop]
#align metric.is_bounded_iff_nndist Metric.isBounded_iff_nndist

theorem toUniformSpace_eq :
    ‚ÄπPseudoMetricSpace Œ±‚Ä∫.toUniformSpace = .ofDist dist dist_self dist_comm dist_triangle :=
  UniformSpace.ext PseudoMetricSpace.uniformity_dist
#align metric.to_uniform_space_eq Metric.toUniformSpace_eq

theorem uniformity_basis_dist :
    (ùì§ Œ±).HasBasis (fun Œµ : ‚Ñù => 0 < Œµ) fun Œµ => { p : Œ± √ó Œ± | dist p.1 p.2 < Œµ } := by
  rw [toUniformSpace_eq]
  exact UniformSpace.hasBasis_ofFun (exists_gt _) _ _ _ _ _
#align metric.uniformity_basis_dist Metric.uniformity_basis_dist

/-- Given `f : Œ≤ ‚Üí ‚Ñù`, if `f` sends `{i | p i}` to a set of positive numbers
accumulating to zero, then `f i`-neighborhoods of the diagonal form a basis of `ùì§ Œ±`.

For specific bases see `uniformity_basis_dist`, `uniformity_basis_dist_inv_nat_succ`,
and `uniformity_basis_dist_inv_nat_pos`. -/
protected theorem mk_uniformity_basis {Œ≤ : Type*} {p : Œ≤ ‚Üí Prop} {f : Œ≤ ‚Üí ‚Ñù}
    (hf‚ÇÄ : ‚àÄ i, p i ‚Üí 0 < f i) (hf : ‚àÄ ‚¶ÉŒµ‚¶Ñ, 0 < Œµ ‚Üí ‚àÉ i, p i ‚àß f i ‚â§ Œµ) :
    (ùì§ Œ±).HasBasis p fun i => { p : Œ± √ó Œ± | dist p.1 p.2 < f i } := by
  refine' ‚ü®fun s => uniformity_basis_dist.mem_iff.trans _‚ü©
  constructor
  ¬∑ rintro ‚ü®Œµ, Œµ‚ÇÄ, hŒµ‚ü©
    rcases hf Œµ‚ÇÄ with ‚ü®i, hi, H‚ü©
    exact ‚ü®i, hi, fun x (hx : _ < _) => hŒµ <| lt_of_lt_of_le hx H‚ü©
  ¬∑ exact fun ‚ü®i, hi, H‚ü© => ‚ü®f i, hf‚ÇÄ i hi, H‚ü©
#align metric.mk_uniformity_basis Metric.mk_uniformity_basis

theorem uniformity_basis_dist_rat :
    (ùì§ Œ±).HasBasis (fun r : ‚Ñö => 0 < r) fun r => { p : Œ± √ó Œ± | dist p.1 p.2 < r } :=
  Metric.mk_uniformity_basis (fun _ => Rat.cast_pos.2) fun _Œµ hŒµ =>
    let ‚ü®r, hr0, hrŒµ‚ü© := exists_rat_btwn hŒµ
    ‚ü®r, Rat.cast_pos.1 hr0, hrŒµ.le‚ü©
#align metric.uniformity_basis_dist_rat Metric.uniformity_basis_dist_rat

theorem uniformity_basis_dist_inv_nat_succ :
    (ùì§ Œ±).HasBasis (fun _ => True) fun n : ‚Ñï => { p : Œ± √ó Œ± | dist p.1 p.2 < 1 / (‚Üën + 1) } :=
  Metric.mk_uniformity_basis (fun n _ => div_pos zero_lt_one <| Nat.cast_add_one_pos n) fun _Œµ Œµ0 =>
    (exists_nat_one_div_lt Œµ0).imp fun _n hn => ‚ü®trivial, le_of_lt hn‚ü©
#align metric.uniformity_basis_dist_inv_nat_succ Metric.uniformity_basis_dist_inv_nat_succ

theorem uniformity_basis_dist_inv_nat_pos :
    (ùì§ Œ±).HasBasis (fun n : ‚Ñï => 0 < n) fun n : ‚Ñï => { p : Œ± √ó Œ± | dist p.1 p.2 < 1 / ‚Üën } :=
  Metric.mk_uniformity_basis (fun n hn => div_pos zero_lt_one <| Nat.cast_pos.2 hn) fun Œµ Œµ0 =>
    let ‚ü®n, hn‚ü© := exists_nat_one_div_lt Œµ0
    ‚ü®n + 1, Nat.succ_pos n, by exact_mod_cast hn.le‚ü©
#align metric.uniformity_basis_dist_inv_nat_pos Metric.uniformity_basis_dist_inv_nat_pos

theorem uniformity_basis_dist_pow {r : ‚Ñù} (h0 : 0 < r) (h1 : r < 1) :
    (ùì§ Œ±).HasBasis (fun _ : ‚Ñï => True) fun n : ‚Ñï => { p : Œ± √ó Œ± | dist p.1 p.2 < r ^ n } :=
  Metric.mk_uniformity_basis (fun _ _ => pow_pos h0 _) fun _Œµ Œµ0 =>
    let ‚ü®n, hn‚ü© := exists_pow_lt_of_lt_one Œµ0 h1
    ‚ü®n, trivial, hn.le‚ü©
#align metric.uniformity_basis_dist_pow Metric.uniformity_basis_dist_pow

theorem uniformity_basis_dist_lt {R : ‚Ñù} (hR : 0 < R) :
    (ùì§ Œ±).HasBasis (fun r : ‚Ñù => 0 < r ‚àß r < R) fun r => { p : Œ± √ó Œ± | dist p.1 p.2 < r } :=
  Metric.mk_uniformity_basis (fun _ => And.left) fun r hr =>
    ‚ü®min r (R / 2), ‚ü®lt_min hr (half_pos hR), min_lt_iff.2 <| Or.inr (half_lt_self hR)‚ü©,
      min_le_left _ _‚ü©
#align metric.uniformity_basis_dist_lt Metric.uniformity_basis_dist_lt

/-- Given `f : Œ≤ ‚Üí ‚Ñù`, if `f` sends `{i | p i}` to a set of positive numbers
accumulating to zero, then closed neighborhoods of the diagonal of sizes `{f i | p i}`
form a basis of `ùì§ Œ±`.

Currently we have only one specific basis `uniformity_basis_dist_le` based on this constructor.
More can be easily added if needed in the future. -/
protected theorem mk_uniformity_basis_le {Œ≤ : Type*} {p : Œ≤ ‚Üí Prop} {f : Œ≤ ‚Üí ‚Ñù}
    (hf‚ÇÄ : ‚àÄ x, p x ‚Üí 0 < f x) (hf : ‚àÄ Œµ, 0 < Œµ ‚Üí ‚àÉ x, p x ‚àß f x ‚â§ Œµ) :
    (ùì§ Œ±).HasBasis p fun x => { p : Œ± √ó Œ± | dist p.1 p.2 ‚â§ f x } := by
  refine' ‚ü®fun s => uniformity_basis_dist.mem_iff.trans _‚ü©
  constructor
  ¬∑ rintro ‚ü®Œµ, Œµ‚ÇÄ, hŒµ‚ü©
    rcases exists_between Œµ‚ÇÄ with ‚ü®Œµ', hŒµ'‚ü©
    rcases hf Œµ' hŒµ'.1 with ‚ü®i, hi, H‚ü©
    exact ‚ü®i, hi, fun x (hx : _ ‚â§ _) => hŒµ <| lt_of_le_of_lt (le_trans hx H) hŒµ'.2‚ü©
  ¬∑ exact fun ‚ü®i, hi, H‚ü© => ‚ü®f i, hf‚ÇÄ i hi, fun x (hx : _ < _) => H (mem_setOf.2 hx.le)‚ü©
#align metric.mk_uniformity_basis_le Metric.mk_uniformity_basis_le

/-- Constant size closed neighborhoods of the diagonal form a basis
of the uniformity filter. -/
theorem uniformity_basis_dist_le :
    (ùì§ Œ±).HasBasis ((0 : ‚Ñù) < ¬∑) fun Œµ => { p : Œ± √ó Œ± | dist p.1 p.2 ‚â§ Œµ } :=
  Metric.mk_uniformity_basis_le (fun _ => id) fun Œµ Œµ‚ÇÄ => ‚ü®Œµ, Œµ‚ÇÄ, le_refl Œµ‚ü©
#align metric.uniformity_basis_dist_le Metric.uniformity_basis_dist_le

theorem uniformity_basis_dist_le_pow {r : ‚Ñù} (h0 : 0 < r) (h1 : r < 1) :
    (ùì§ Œ±).HasBasis (fun _ : ‚Ñï => True) fun n : ‚Ñï => { p : Œ± √ó Œ± | dist p.1 p.2 ‚â§ r ^ n } :=
  Metric.mk_uniformity_basis_le (fun _ _ => pow_pos h0 _) fun _Œµ Œµ0 =>
    let ‚ü®n, hn‚ü© := exists_pow_lt_of_lt_one Œµ0 h1
    ‚ü®n, trivial, hn.le‚ü©
#align metric.uniformity_basis_dist_le_pow Metric.uniformity_basis_dist_le_pow

theorem mem_uniformity_dist {s : Set (Œ± √ó Œ±)} :
    s ‚àà ùì§ Œ± ‚Üî ‚àÉ Œµ > 0, ‚àÄ {a b : Œ±}, dist a b < Œµ ‚Üí (a, b) ‚àà s :=
  uniformity_basis_dist.mem_uniformity_iff
#align metric.mem_uniformity_dist Metric.mem_uniformity_dist

/-- A constant size neighborhood of the diagonal is an entourage. -/
theorem dist_mem_uniformity {Œµ : ‚Ñù} (Œµ0 : 0 < Œµ) : { p : Œ± √ó Œ± | dist p.1 p.2 < Œµ } ‚àà ùì§ Œ± :=
  mem_uniformity_dist.2 ‚ü®Œµ, Œµ0, id‚ü©
#align metric.dist_mem_uniformity Metric.dist_mem_uniformity

theorem uniformContinuous_iff [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} :
    UniformContinuous f ‚Üî ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ {a b : Œ±}, dist a b < Œ¥ ‚Üí dist (f a) (f b) < Œµ :=
  uniformity_basis_dist.uniformContinuous_iff uniformity_basis_dist
#align metric.uniform_continuous_iff Metric.uniformContinuous_iff

theorem uniformContinuousOn_iff [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ±} :
    UniformContinuousOn f s ‚Üî
      ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, dist x y < Œ¥ ‚Üí dist (f x) (f y) < Œµ :=
  Metric.uniformity_basis_dist.uniformContinuousOn_iff Metric.uniformity_basis_dist
#align metric.uniform_continuous_on_iff Metric.uniformContinuousOn_iff

theorem uniformContinuousOn_iff_le [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ±} :
    UniformContinuousOn f s ‚Üî
      ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, dist x y ‚â§ Œ¥ ‚Üí dist (f x) (f y) ‚â§ Œµ :=
  Metric.uniformity_basis_dist_le.uniformContinuousOn_iff Metric.uniformity_basis_dist_le
#align metric.uniform_continuous_on_iff_le Metric.uniformContinuousOn_iff_le

nonrec theorem uniformInducing_iff [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} :
    UniformInducing f ‚Üî UniformContinuous f ‚àß
      ‚àÄ Œ¥ > 0, ‚àÉ Œµ > 0, ‚àÄ {a b : Œ±}, dist (f a) (f b) < Œµ ‚Üí dist a b < Œ¥ :=
  uniformInducing_iff'.trans <| Iff.rfl.and <|
    ((uniformity_basis_dist.comap _).le_basis_iff uniformity_basis_dist).trans <| by
      simp only [subset_def, Prod.forall, gt_iff_lt, preimage_setOf_eq, Prod_map, mem_setOf]

nonrec theorem uniformEmbedding_iff [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} :
    UniformEmbedding f ‚Üî Function.Injective f ‚àß UniformContinuous f ‚àß
      ‚àÄ Œ¥ > 0, ‚àÉ Œµ > 0, ‚àÄ {a b : Œ±}, dist (f a) (f b) < Œµ ‚Üí dist a b < Œ¥ := by
  rw [uniformEmbedding_iff, and_comm, uniformInducing_iff]
#align metric.uniform_embedding_iff Metric.uniformEmbedding_iff

/-- If a map between pseudometric spaces is a uniform embedding then the distance between `f x`
and `f y` is controlled in terms of the distance between `x` and `y`. -/
theorem controlled_of_uniformEmbedding [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} (h : UniformEmbedding f) :
    (‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ {a b : Œ±}, dist a b < Œ¥ ‚Üí dist (f a) (f b) < Œµ) ‚àß
      ‚àÄ Œ¥ > 0, ‚àÉ Œµ > 0, ‚àÄ {a b : Œ±}, dist (f a) (f b) < Œµ ‚Üí dist a b < Œ¥ :=
  ‚ü®uniformContinuous_iff.1 h.uniformContinuous, (uniformEmbedding_iff.1 h).2.2‚ü©
#align metric.controlled_of_uniform_embedding Metric.controlled_of_uniformEmbedding

theorem totallyBounded_iff {s : Set Œ±} :
    TotallyBounded s ‚Üî ‚àÄ Œµ > 0, ‚àÉ t : Set Œ±, t.Finite ‚àß s ‚äÜ ‚ãÉ y ‚àà t, ball y Œµ :=
  uniformity_basis_dist.totallyBounded_iff
#align metric.totally_bounded_iff Metric.totallyBounded_iff

/-- A pseudometric space is totally bounded if one can reconstruct up to any Œµ>0 any element of the
space from finitely many data. -/
theorem totallyBounded_of_finite_discretization {s : Set Œ±}
    (H : ‚àÄ Œµ > (0 : ‚Ñù),
        ‚àÉ (Œ≤ : Type u) (_ : Fintype Œ≤) (F : s ‚Üí Œ≤), ‚àÄ x y, F x = F y ‚Üí dist (x : Œ±) y < Œµ) :
    TotallyBounded s := by
  cases' s.eq_empty_or_nonempty with hs hs
  ¬∑ rw [hs]
    exact totallyBounded_empty
  rcases hs with ‚ü®x0, hx0‚ü©
  haveI : Inhabited s := ‚ü®‚ü®x0, hx0‚ü©‚ü©
  refine' totallyBounded_iff.2 fun Œµ Œµ0 => _
  rcases H Œµ Œµ0 with ‚ü®Œ≤, fŒ≤, F, hF‚ü©
  let Finv := Function.invFun F
  refine' ‚ü®range (Subtype.val ‚àò Finv), finite_range _, fun x xs => _‚ü©
  let x' := Finv (F ‚ü®x, xs‚ü©)
  have : F x' = F ‚ü®x, xs‚ü© := Function.invFun_eq ‚ü®‚ü®x, xs‚ü©, rfl‚ü©
  simp only [Set.mem_iUnion, Set.mem_range]
  exact ‚ü®_, ‚ü®F ‚ü®x, xs‚ü©, rfl‚ü©, hF _ _ this.symm‚ü©
#align metric.totally_bounded_of_finite_discretization Metric.totallyBounded_of_finite_discretization

theorem finite_approx_of_totallyBounded {s : Set Œ±} (hs : TotallyBounded s) :
    ‚àÄ Œµ > 0, ‚àÉ t, t ‚äÜ s ‚àß Set.Finite t ‚àß s ‚äÜ ‚ãÉ y ‚àà t, ball y Œµ := by
  intro Œµ Œµ_pos
  rw [totallyBounded_iff_subset] at hs
  exact hs _ (dist_mem_uniformity Œµ_pos)
#align metric.finite_approx_of_totally_bounded Metric.finite_approx_of_totallyBounded

/-- Expressing uniform convergence using `dist` -/
theorem tendstoUniformlyOnFilter_iff {F : Œπ ‚Üí Œ≤ ‚Üí Œ±} {f : Œ≤ ‚Üí Œ±} {p : Filter Œπ} {p' : Filter Œ≤} :
    TendstoUniformlyOnFilter F f p p' ‚Üî
      ‚àÄ Œµ > 0, ‚àÄ·∂† n : Œπ √ó Œ≤ in p √óÀ¢ p', dist (f n.snd) (F n.fst n.snd) < Œµ := by
  refine' ‚ü®fun H Œµ hŒµ => H _ (dist_mem_uniformity hŒµ), fun H u hu => _‚ü©
  rcases mem_uniformity_dist.1 hu with ‚ü®Œµ, Œµpos, hŒµ‚ü©
  refine' (H Œµ Œµpos).mono fun n hn => hŒµ hn
#align metric.tendsto_uniformly_on_filter_iff Metric.tendstoUniformlyOnFilter_iff

/-- Expressing locally uniform convergence on a set using `dist`. -/
theorem tendstoLocallyUniformlyOn_iff [TopologicalSpace Œ≤] {F : Œπ ‚Üí Œ≤ ‚Üí Œ±} {f : Œ≤ ‚Üí Œ±}
    {p : Filter Œπ} {s : Set Œ≤} :
    TendstoLocallyUniformlyOn F f p s ‚Üî
      ‚àÄ Œµ > 0, ‚àÄ x ‚àà s, ‚àÉ t ‚àà ùìù[s] x, ‚àÄ·∂† n in p, ‚àÄ y ‚àà t, dist (f y) (F n y) < Œµ := by
  refine' ‚ü®fun H Œµ hŒµ => H _ (dist_mem_uniformity hŒµ), fun H u hu x hx => _‚ü©
  rcases mem_uniformity_dist.1 hu with ‚ü®Œµ, Œµpos, hŒµ‚ü©
  rcases H Œµ Œµpos x hx with ‚ü®t, ht, Ht‚ü©
  exact ‚ü®t, ht, Ht.mono fun n hs x hx => hŒµ (hs x hx)‚ü©
#align metric.tendsto_locally_uniformly_on_iff Metric.tendstoLocallyUniformlyOn_iff

/-- Expressing uniform convergence on a set using `dist`. -/
theorem tendstoUniformlyOn_iff {F : Œπ ‚Üí Œ≤ ‚Üí Œ±} {f : Œ≤ ‚Üí Œ±} {p : Filter Œπ} {s : Set Œ≤} :
    TendstoUniformlyOn F f p s ‚Üî ‚àÄ Œµ > 0, ‚àÄ·∂† n in p, ‚àÄ x ‚àà s, dist (f x) (F n x) < Œµ := by
  refine' ‚ü®fun H Œµ hŒµ => H _ (dist_mem_uniformity hŒµ), fun H u hu => _‚ü©
  rcases mem_uniformity_dist.1 hu with ‚ü®Œµ, Œµpos, hŒµ‚ü©
  exact (H Œµ Œµpos).mono fun n hs x hx => hŒµ (hs x hx)
#align metric.tendsto_uniformly_on_iff Metric.tendstoUniformlyOn_iff

/-- Expressing locally uniform convergence using `dist`. -/
theorem tendstoLocallyUniformly_iff [TopologicalSpace Œ≤] {F : Œπ ‚Üí Œ≤ ‚Üí Œ±} {f : Œ≤ ‚Üí Œ±}
    {p : Filter Œπ} :
    TendstoLocallyUniformly F f p ‚Üî
      ‚àÄ Œµ > 0, ‚àÄ x : Œ≤, ‚àÉ t ‚àà ùìù x, ‚àÄ·∂† n in p, ‚àÄ y ‚àà t, dist (f y) (F n y) < Œµ := by
  simp only [‚Üê tendstoLocallyUniformlyOn_univ, tendstoLocallyUniformlyOn_iff, nhdsWithin_univ,
    mem_univ, forall_const, exists_prop]
#align metric.tendsto_locally_uniformly_iff Metric.tendstoLocallyUniformly_iff

/-- Expressing uniform convergence using `dist`. -/
theorem tendstoUniformly_iff {F : Œπ ‚Üí Œ≤ ‚Üí Œ±} {f : Œ≤ ‚Üí Œ±} {p : Filter Œπ} :
    TendstoUniformly F f p ‚Üî ‚àÄ Œµ > 0, ‚àÄ·∂† n in p, ‚àÄ x, dist (f x) (F n x) < Œµ := by
  rw [‚Üê tendstoUniformlyOn_univ, tendstoUniformlyOn_iff]
  simp
#align metric.tendsto_uniformly_iff Metric.tendstoUniformly_iff

protected theorem cauchy_iff {f : Filter Œ±} :
    Cauchy f ‚Üî NeBot f ‚àß ‚àÄ Œµ > 0, ‚àÉ t ‚àà f, ‚àÄ x ‚àà t, ‚àÄ y ‚àà t, dist x y < Œµ :=
  uniformity_basis_dist.cauchy_iff
#align metric.cauchy_iff Metric.cauchy_iff

theorem nhds_basis_ball : (ùìù x).HasBasis (0 < ¬∑) (ball x) :=
  nhds_basis_uniformity uniformity_basis_dist
#align metric.nhds_basis_ball Metric.nhds_basis_ball

theorem mem_nhds_iff : s ‚àà ùìù x ‚Üî ‚àÉ Œµ > 0, ball x Œµ ‚äÜ s :=
  nhds_basis_ball.mem_iff
#align metric.mem_nhds_iff Metric.mem_nhds_iff

theorem eventually_nhds_iff {p : Œ± ‚Üí Prop} :
    (‚àÄ·∂† y in ùìù x, p y) ‚Üî ‚àÉ Œµ > 0, ‚àÄ ‚¶Éy‚¶Ñ, dist y x < Œµ ‚Üí p y :=
  mem_nhds_iff
#align metric.eventually_nhds_iff Metric.eventually_nhds_iff

theorem eventually_nhds_iff_ball {p : Œ± ‚Üí Prop} :
    (‚àÄ·∂† y in ùìù x, p y) ‚Üî ‚àÉ Œµ > 0, ‚àÄ y ‚àà ball x Œµ, p y :=
  mem_nhds_iff
#align metric.eventually_nhds_iff_ball Metric.eventually_nhds_iff_ball

/-- A version of `Filter.eventually_prod_iff` where the first filter consists of neighborhoods
in a pseudo-metric space.-/
theorem eventually_nhds_prod_iff {f : Filter Œπ} {x‚ÇÄ : Œ±} {p : Œ± √ó Œπ ‚Üí Prop} :
    (‚àÄ·∂† x in ùìù x‚ÇÄ √óÀ¢ f, p x) ‚Üî ‚àÉ Œµ > (0 : ‚Ñù), ‚àÉ pa : Œπ ‚Üí Prop, (‚àÄ·∂† i in f, pa i) ‚àß
      ‚àÄ {x}, dist x x‚ÇÄ < Œµ ‚Üí ‚àÄ {i}, pa i ‚Üí p (x, i) := by
  refine (nhds_basis_ball.prod f.basis_sets).eventually_iff.trans ?_
  simp only [Prod.exists, forall_prod_set, id, mem_ball, and_assoc, exists_and_left, and_imp]
  rfl
#align metric.eventually_nhds_prod_iff Metric.eventually_nhds_prod_iff

/-- A version of `Filter.eventually_prod_iff` where the second filter consists of neighborhoods
in a pseudo-metric space.-/
theorem eventually_prod_nhds_iff {f : Filter Œπ} {x‚ÇÄ : Œ±} {p : Œπ √ó Œ± ‚Üí Prop} :
    (‚àÄ·∂† x in f √óÀ¢ ùìù x‚ÇÄ, p x) ‚Üî ‚àÉ pa : Œπ ‚Üí Prop, (‚àÄ·∂† i in f, pa i) ‚àß
      ‚àÉ Œµ > 0, ‚àÄ {i}, pa i ‚Üí ‚àÄ {x}, dist x x‚ÇÄ < Œµ ‚Üí p (i, x) := by
  rw [eventually_swap_iff, Metric.eventually_nhds_prod_iff]
  constructor <;>
    ¬∑ rintro ‚ü®a1, a2, a3, a4, a5‚ü©
      exact ‚ü®a3, a4, a1, a2, fun b1 b2 b3 => a5 b3 b1‚ü©
#align metric.eventually_prod_nhds_iff Metric.eventually_prod_nhds_iff

theorem nhds_basis_closedBall : (ùìù x).HasBasis (fun Œµ : ‚Ñù => 0 < Œµ) (closedBall x) :=
  nhds_basis_uniformity uniformity_basis_dist_le
#align metric.nhds_basis_closed_ball Metric.nhds_basis_closedBall

theorem nhds_basis_ball_inv_nat_succ :
    (ùìù x).HasBasis (fun _ => True) fun n : ‚Ñï => ball x (1 / (‚Üën + 1)) :=
  nhds_basis_uniformity uniformity_basis_dist_inv_nat_succ
#align metric.nhds_basis_ball_inv_nat_succ Metric.nhds_basis_ball_inv_nat_succ

theorem nhds_basis_ball_inv_nat_pos :
    (ùìù x).HasBasis (fun n => 0 < n) fun n : ‚Ñï => ball x (1 / ‚Üën) :=
  nhds_basis_uniformity uniformity_basis_dist_inv_nat_pos
#align metric.nhds_basis_ball_inv_nat_pos Metric.nhds_basis_ball_inv_nat_pos

theorem nhds_basis_ball_pow {r : ‚Ñù} (h0 : 0 < r) (h1 : r < 1) :
    (ùìù x).HasBasis (fun _ => True) fun n : ‚Ñï => ball x (r ^ n) :=
  nhds_basis_uniformity (uniformity_basis_dist_pow h0 h1)
#align metric.nhds_basis_ball_pow Metric.nhds_basis_ball_pow

theorem nhds_basis_closedBall_pow {r : ‚Ñù} (h0 : 0 < r) (h1 : r < 1) :
    (ùìù x).HasBasis (fun _ => True) fun n : ‚Ñï => closedBall x (r ^ n) :=
  nhds_basis_uniformity (uniformity_basis_dist_le_pow h0 h1)
#align metric.nhds_basis_closed_ball_pow Metric.nhds_basis_closedBall_pow

theorem isOpen_iff : IsOpen s ‚Üî ‚àÄ x ‚àà s, ‚àÉ Œµ > 0, ball x Œµ ‚äÜ s := by
  simp only [isOpen_iff_mem_nhds, mem_nhds_iff]
#align metric.is_open_iff Metric.isOpen_iff

theorem isOpen_ball : IsOpen (ball x Œµ) :=
  isOpen_iff.2 fun _ => exists_ball_subset_ball
#align metric.is_open_ball Metric.isOpen_ball

theorem ball_mem_nhds (x : Œ±) {Œµ : ‚Ñù} (Œµ0 : 0 < Œµ) : ball x Œµ ‚àà ùìù x :=
  isOpen_ball.mem_nhds (mem_ball_self Œµ0)
#align metric.ball_mem_nhds Metric.ball_mem_nhds

theorem closedBall_mem_nhds (x : Œ±) {Œµ : ‚Ñù} (Œµ0 : 0 < Œµ) : closedBall x Œµ ‚àà ùìù x :=
  mem_of_superset (ball_mem_nhds x Œµ0) ball_subset_closedBall
#align metric.closed_ball_mem_nhds Metric.closedBall_mem_nhds

theorem closedBall_mem_nhds_of_mem {x c : Œ±} {Œµ : ‚Ñù} (h : x ‚àà ball c Œµ) : closedBall c Œµ ‚àà ùìù x :=
  mem_of_superset (isOpen_ball.mem_nhds h) ball_subset_closedBall
#align metric.closed_ball_mem_nhds_of_mem Metric.closedBall_mem_nhds_of_mem

theorem nhdsWithin_basis_ball {s : Set Œ±} :
    (ùìù[s] x).HasBasis (fun Œµ : ‚Ñù => 0 < Œµ) fun Œµ => ball x Œµ ‚à© s :=
  nhdsWithin_hasBasis nhds_basis_ball s
#align metric.nhds_within_basis_ball Metric.nhdsWithin_basis_ball

theorem mem_nhdsWithin_iff {t : Set Œ±} : s ‚àà ùìù[t] x ‚Üî ‚àÉ Œµ > 0, ball x Œµ ‚à© t ‚äÜ s :=
  nhdsWithin_basis_ball.mem_iff
#align metric.mem_nhds_within_iff Metric.mem_nhdsWithin_iff

theorem tendsto_nhdsWithin_nhdsWithin [PseudoMetricSpace Œ≤] {t : Set Œ≤} {f : Œ± ‚Üí Œ≤} {a b} :
    Tendsto f (ùìù[s] a) (ùìù[t] b) ‚Üî
      ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ {x : Œ±}, x ‚àà s ‚Üí dist x a < Œ¥ ‚Üí f x ‚àà t ‚àß dist (f x) b < Œµ :=
  (nhdsWithin_basis_ball.tendsto_iff nhdsWithin_basis_ball).trans <| by
    simp only [inter_comm _ s, inter_comm _ t, mem_inter_iff, and_imp]; rfl
#align metric.tendsto_nhds_within_nhds_within Metric.tendsto_nhdsWithin_nhdsWithin

theorem tendsto_nhdsWithin_nhds [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} {a b} :
    Tendsto f (ùìù[s] a) (ùìù b) ‚Üî
      ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ {x : Œ±}, x ‚àà s ‚Üí dist x a < Œ¥ ‚Üí dist (f x) b < Œµ := by
  rw [‚Üê nhdsWithin_univ b, tendsto_nhdsWithin_nhdsWithin]
  simp only [mem_univ, true_and_iff]
#align metric.tendsto_nhds_within_nhds Metric.tendsto_nhdsWithin_nhds

theorem tendsto_nhds_nhds [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} {a b} :
    Tendsto f (ùìù a) (ùìù b) ‚Üî ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ {x : Œ±}, dist x a < Œ¥ ‚Üí dist (f x) b < Œµ :=
  nhds_basis_ball.tendsto_iff nhds_basis_ball
#align metric.tendsto_nhds_nhds Metric.tendsto_nhds_nhds

theorem continuousAt_iff [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} {a : Œ±} :
    ContinuousAt f a ‚Üî ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ {x : Œ±}, dist x a < Œ¥ ‚Üí dist (f x) (f a) < Œµ := by
  rw [ContinuousAt, tendsto_nhds_nhds]
#align metric.continuous_at_iff Metric.continuousAt_iff

theorem continuousWithinAt_iff [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} {a : Œ±} {s : Set Œ±} :
    ContinuousWithinAt f s a ‚Üî
      ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ {x : Œ±}, x ‚àà s ‚Üí dist x a < Œ¥ ‚Üí dist (f x) (f a) < Œµ :=
  by rw [ContinuousWithinAt, tendsto_nhdsWithin_nhds]
#align metric.continuous_within_at_iff Metric.continuousWithinAt_iff

theorem continuousOn_iff [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ±} :
    ContinuousOn f s ‚Üî ‚àÄ b ‚àà s, ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ a ‚àà s, dist a b < Œ¥ ‚Üí dist (f a) (f b) < Œµ := by
  simp [ContinuousOn, continuousWithinAt_iff]
#align metric.continuous_on_iff Metric.continuousOn_iff

theorem continuous_iff [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} :
    Continuous f ‚Üî ‚àÄ b, ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ a, dist a b < Œ¥ ‚Üí dist (f a) (f b) < Œµ :=
  continuous_iff_continuousAt.trans <| forall_congr' fun _ => tendsto_nhds_nhds
#align metric.continuous_iff Metric.continuous_iff

theorem tendsto_nhds {f : Filter Œ≤} {u : Œ≤ ‚Üí Œ±} {a : Œ±} :
    Tendsto u f (ùìù a) ‚Üî ‚àÄ Œµ > 0, ‚àÄ·∂† x in f, dist (u x) a < Œµ :=
  nhds_basis_ball.tendsto_right_iff
#align metric.tendsto_nhds Metric.tendsto_nhds

theorem continuousAt_iff' [TopologicalSpace Œ≤] {f : Œ≤ ‚Üí Œ±} {b : Œ≤} :
    ContinuousAt f b ‚Üî ‚àÄ Œµ > 0, ‚àÄ·∂† x in ùìù b, dist (f x) (f b) < Œµ := by
  rw [ContinuousAt, tendsto_nhds]
#align metric.continuous_at_iff' Metric.continuousAt_iff'

theorem continuousWithinAt_iff' [TopologicalSpace Œ≤] {f : Œ≤ ‚Üí Œ±} {b : Œ≤} {s : Set Œ≤} :
    ContinuousWithinAt f s b ‚Üî ‚àÄ Œµ > 0, ‚àÄ·∂† x in ùìù[s] b, dist (f x) (f b) < Œµ := by
  rw [ContinuousWithinAt, tendsto_nhds]
#align metric.continuous_within_at_iff' Metric.continuousWithinAt_iff'

theorem continuousOn_iff' [TopologicalSpace Œ≤] {f : Œ≤ ‚Üí Œ±} {s : Set Œ≤} :
    ContinuousOn f s ‚Üî ‚àÄ b ‚àà s, ‚àÄ Œµ > 0, ‚àÄ·∂† x in ùìù[s] b, dist (f x) (f b) < Œµ := by
  simp [ContinuousOn, continuousWithinAt_iff']
#align metric.continuous_on_iff' Metric.continuousOn_iff'

theorem continuous_iff' [TopologicalSpace Œ≤] {f : Œ≤ ‚Üí Œ±} :
    Continuous f ‚Üî ‚àÄ (a), ‚àÄ Œµ > 0, ‚àÄ·∂† x in ùìù a, dist (f x) (f a) < Œµ :=
  continuous_iff_continuousAt.trans <| forall_congr' fun _ => tendsto_nhds
#align metric.continuous_iff' Metric.continuous_iff'

theorem tendsto_atTop [Nonempty Œ≤] [SemilatticeSup Œ≤] {u : Œ≤ ‚Üí Œ±} {a : Œ±} :
    Tendsto u atTop (ùìù a) ‚Üî ‚àÄ Œµ > 0, ‚àÉ N, ‚àÄ n ‚â• N, dist (u n) a < Œµ :=
  (atTop_basis.tendsto_iff nhds_basis_ball).trans <| by
    simp only [true_and]; rfl
#align metric.tendsto_at_top Metric.tendsto_atTop

/-- A variant of `tendsto_atTop` that
uses `‚àÉ N, ‚àÄ n > N, ...` rather than `‚àÉ N, ‚àÄ n ‚â• N, ...`
-/
theorem tendsto_atTop' [Nonempty Œ≤] [SemilatticeSup Œ≤] [NoMaxOrder Œ≤] {u : Œ≤ ‚Üí Œ±} {a : Œ±} :
    Tendsto u atTop (ùìù a) ‚Üî ‚àÄ Œµ > 0, ‚àÉ N, ‚àÄ n > N, dist (u n) a < Œµ :=
  (atTop_basis_Ioi.tendsto_iff nhds_basis_ball).trans <| by
    simp only [true_and, gt_iff_lt, mem_Ioi, mem_ball]
#align metric.tendsto_at_top' Metric.tendsto_atTop'

theorem isOpen_singleton_iff {Œ± : Type*} [PseudoMetricSpace Œ±] {x : Œ±} :
    IsOpen ({x} : Set Œ±) ‚Üî ‚àÉ Œµ > 0, ‚àÄ y, dist y x < Œµ ‚Üí y = x := by
  simp [isOpen_iff, subset_singleton_iff, mem_ball]
#align metric.is_open_singleton_iff Metric.isOpen_singleton_iff

/-- Given a point `x` in a discrete subset `s` of a pseudometric space, there is an open ball
centered at `x` and intersecting `s` only at `x`. -/
theorem exists_ball_inter_eq_singleton_of_mem_discrete [DiscreteTopology s] {x : Œ±} (hx : x ‚àà s) :
    ‚àÉ Œµ > 0, Metric.ball x Œµ ‚à© s = {x} :=
  nhds_basis_ball.exists_inter_eq_singleton_of_mem_discrete hx
#align metric.exists_ball_inter_eq_singleton_of_mem_discrete Metric.exists_ball_inter_eq_singleton_of_mem_discrete

/-- Given a point `x` in a discrete subset `s` of a pseudometric space, there is a closed ball
of positive radius centered at `x` and intersecting `s` only at `x`. -/
theorem exists_closedBall_inter_eq_singleton_of_discrete [DiscreteTopology s] {x : Œ±} (hx : x ‚àà s) :
    ‚àÉ Œµ > 0, Metric.closedBall x Œµ ‚à© s = {x} :=
  nhds_basis_closedBall.exists_inter_eq_singleton_of_mem_discrete hx
#align metric.exists_closed_ball_inter_eq_singleton_of_discrete Metric.exists_closedBall_inter_eq_singleton_of_discrete

theorem _root_.Dense.exists_dist_lt {s : Set Œ±} (hs : Dense s) (x : Œ±) {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) :
    ‚àÉ y ‚àà s, dist x y < Œµ := by
  have : (ball x Œµ).Nonempty := by simp [hŒµ]
  simpa only [mem_ball'] using hs.exists_mem_open isOpen_ball this
#align dense.exists_dist_lt Dense.exists_dist_lt

nonrec theorem _root_.DenseRange.exists_dist_lt {Œ≤ : Type*} {f : Œ≤ ‚Üí Œ±} (hf : DenseRange f) (x : Œ±)
    {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) : ‚àÉ y, dist x (f y) < Œµ :=
  exists_range_iff.1 (hf.exists_dist_lt x hŒµ)
#align dense_range.exists_dist_lt DenseRange.exists_dist_lt

end Metric

open Metric

/-Instantiate a pseudometric space as a pseudoemetric space. Before we can state the instance,
we need to show that the uniform structure coming from the edistance and the
distance coincide. -/

-- porting note: new
theorem Metric.uniformity_edist_aux {Œ±} (d : Œ± ‚Üí Œ± ‚Üí ‚Ñù‚â•0) :
    ‚®Ö Œµ > (0 : ‚Ñù), ùìü { p : Œ± √ó Œ± | ‚Üë(d p.1 p.2) < Œµ } =
      ‚®Ö Œµ > (0 : ‚Ñù‚â•0‚àû), ùìü { p : Œ± √ó Œ± | ‚Üë(d p.1 p.2) < Œµ } := by
  simp only [le_antisymm_iff, le_iInf_iff, le_principal_iff]
  refine ‚ü®fun Œµ hŒµ => ?_, fun Œµ hŒµ => ?_‚ü©
  ¬∑ rcases ENNReal.lt_iff_exists_nnreal_btwn.1 hŒµ with ‚ü®Œµ', Œµ'0, Œµ'Œµ‚ü©
    refine mem_iInf_of_mem (Œµ' : ‚Ñù) (mem_iInf_of_mem (ENNReal.coe_pos.1 Œµ'0) ?_)
    exact fun x hx => lt_trans (ENNReal.coe_lt_coe.2 hx) Œµ'Œµ
  ¬∑ lift Œµ to ‚Ñù‚â•0 using le_of_lt hŒµ
    refine mem_iInf_of_mem (Œµ : ‚Ñù‚â•0‚àû) (mem_iInf_of_mem (ENNReal.coe_pos.2 hŒµ) ?_)
    exact fun _ => ENNReal.coe_lt_coe.1

theorem Metric.uniformity_edist : ùì§ Œ± = ‚®Ö Œµ > 0, ùìü { p : Œ± √ó Œ± | edist p.1 p.2 < Œµ } := by
  simp only [PseudoMetricSpace.uniformity_dist, dist_nndist, edist_nndist,
    Metric.uniformity_edist_aux]
#align metric.uniformity_edist Metric.uniformity_edist

-- see Note [lower instance priority]
/-- A pseudometric space induces a pseudoemetric space -/
instance (priority := 100) PseudoMetricSpace.toPseudoEMetricSpace : PseudoEMetricSpace Œ± :=
  { ‚ÄπPseudoMetricSpace Œ±‚Ä∫ with
    edist_self := by simp [edist_dist]
    edist_comm := fun _ _ => by simp only [edist_dist, dist_comm]
    edist_triangle := fun x y z => by
      simp only [edist_dist, ‚Üê ENNReal.ofReal_add, dist_nonneg]
      rw [ENNReal.ofReal_le_ofReal_iff _]
      ¬∑ exact dist_triangle _ _ _
      ¬∑ simpa using add_le_add (dist_nonneg : 0 ‚â§ dist x y) dist_nonneg
    uniformity_edist := Metric.uniformity_edist }
#align pseudo_metric_space.to_pseudo_emetric_space PseudoMetricSpace.toPseudoEMetricSpace

/-- Expressing the uniformity in terms of `edist` -/
@[deprecated _root_.uniformity_basis_edist]
protected theorem Metric.uniformity_basis_edist :
    (ùì§ Œ±).HasBasis (fun Œµ : ‚Ñù‚â•0‚àû => 0 < Œµ) fun Œµ => { p | edist p.1 p.2 < Œµ } :=
  uniformity_basis_edist
#align pseudo_metric.uniformity_basis_edist Metric.uniformity_basis_edist

/-- In a pseudometric space, an open ball of infinite radius is the whole space -/
theorem Metric.eball_top_eq_univ (x : Œ±) : EMetric.ball x ‚àû = Set.univ :=
  Set.eq_univ_iff_forall.mpr fun y => edist_lt_top y x
#align metric.eball_top_eq_univ Metric.eball_top_eq_univ

/-- Balls defined using the distance or the edistance coincide -/
@[simp]
theorem Metric.emetric_ball {x : Œ±} {Œµ : ‚Ñù} : EMetric.ball x (ENNReal.ofReal Œµ) = ball x Œµ := by
  ext y
  simp only [EMetric.mem_ball, mem_ball, edist_dist]
  exact ENNReal.ofReal_lt_ofReal_iff_of_nonneg dist_nonneg
#align metric.emetric_ball Metric.emetric_ball

/-- Balls defined using the distance or the edistance coincide -/
@[simp]
theorem Metric.emetric_ball_nnreal {x : Œ±} {Œµ : ‚Ñù‚â•0} : EMetric.ball x Œµ = ball x Œµ := by
  rw [‚Üê Metric.emetric_ball]
  simp
#align metric.emetric_ball_nnreal Metric.emetric_ball_nnreal

/-- Closed balls defined using the distance or the edistance coincide -/
theorem Metric.emetric_closedBall {x : Œ±} {Œµ : ‚Ñù} (h : 0 ‚â§ Œµ) :
    EMetric.closedBall x (ENNReal.ofReal Œµ) = closedBall x Œµ := by
  ext y; simp [edist_le_ofReal h]
#align metric.emetric_closed_ball Metric.emetric_closedBall

/-- Closed balls defined using the distance or the edistance coincide -/
@[simp]
theorem Metric.emetric_closedBall_nnreal {x : Œ±} {Œµ : ‚Ñù‚â•0} :
    EMetric.closedBall x Œµ = closedBall x Œµ := by
  rw [‚Üê Metric.emetric_closedBall Œµ.coe_nonneg, ENNReal.ofReal_coe_nnreal]
#align metric.emetric_closed_ball_nnreal Metric.emetric_closedBall_nnreal

@[simp]
theorem Metric.emetric_ball_top (x : Œ±) : EMetric.ball x ‚ä§ = univ :=
  eq_univ_of_forall fun _ => edist_lt_top _ _
#align metric.emetric_ball_top Metric.emetric_ball_top

theorem Metric.inseparable_iff {x y : Œ±} : Inseparable x y ‚Üî dist x y = 0 := by
  rw [EMetric.inseparable_iff, edist_nndist, dist_nndist, ENNReal.coe_eq_zero, NNReal.coe_eq_zero]
#align metric.inseparable_iff Metric.inseparable_iff

/-- Build a new pseudometric space from an old one where the bundled uniform structure is provably
(but typically non-definitionaly) equal to some given uniform structure.
See Note [forgetful inheritance].
-/
@[reducible]
def PseudoMetricSpace.replaceUniformity {Œ±} [U : UniformSpace Œ±] (m : PseudoMetricSpace Œ±)
    (H : ùì§[U] = ùì§[PseudoEMetricSpace.toUniformSpace]) : PseudoMetricSpace Œ± :=
  { m with
    toUniformSpace := U
    uniformity_dist := H.trans PseudoMetricSpace.uniformity_dist }
#align pseudo_metric_space.replace_uniformity PseudoMetricSpace.replaceUniformity

theorem PseudoMetricSpace.replaceUniformity_eq {Œ±} [U : UniformSpace Œ±] (m : PseudoMetricSpace Œ±)
    (H : ùì§[U] = ùì§[PseudoEMetricSpace.toUniformSpace]) : m.replaceUniformity H = m := by
  ext
  rfl
#align pseudo_metric_space.replace_uniformity_eq PseudoMetricSpace.replaceUniformity_eq

-- ensure that the bornology is unchanged when replacing the uniformity.
example {Œ±} [U : UniformSpace Œ±] (m : PseudoMetricSpace Œ±)
    (H : ùì§[U] = ùì§[PseudoEMetricSpace.toUniformSpace]) :
  (PseudoMetricSpace.replaceUniformity m H).toBornology = m.toBornology := rfl

/-- Build a new pseudo metric space from an old one where the bundled topological structure is
provably (but typically non-definitionaly) equal to some given topological structure.
See Note [forgetful inheritance].
-/
@[reducible]
def PseudoMetricSpace.replaceTopology {Œ≥} [U : TopologicalSpace Œ≥] (m : PseudoMetricSpace Œ≥)
    (H : U = m.toUniformSpace.toTopologicalSpace) : PseudoMetricSpace Œ≥ :=
  @PseudoMetricSpace.replaceUniformity Œ≥ (m.toUniformSpace.replaceTopology H) m rfl
#align pseudo_metric_space.replace_topology PseudoMetricSpace.replaceTopology

theorem PseudoMetricSpace.replaceTopology_eq {Œ≥} [U : TopologicalSpace Œ≥] (m : PseudoMetricSpace Œ≥)
    (H : U = m.toUniformSpace.toTopologicalSpace) : m.replaceTopology H = m := by
  ext
  rfl
#align pseudo_metric_space.replace_topology_eq PseudoMetricSpace.replaceTopology_eq

/-- One gets a pseudometric space from an emetric space if the edistance
is everywhere finite, by pushing the edistance to reals. We set it up so that the edist and the
uniformity are defeq in the pseudometric space and the pseudoemetric space. In this definition, the
distance is given separately, to be able to prescribe some expression which is not defeq to the
push-forward of the edistance to reals. -/
def PseudoEMetricSpace.toPseudoMetricSpaceOfDist {Œ± : Type u} [e : PseudoEMetricSpace Œ±]
    (dist : Œ± ‚Üí Œ± ‚Üí ‚Ñù) (edist_ne_top : ‚àÄ x y : Œ±, edist x y ‚â† ‚ä§)
    (h : ‚àÄ x y, dist x y = ENNReal.toReal (edist x y)) : PseudoMetricSpace Œ± where
  dist := dist
  dist_self x := by simp [h]
  dist_comm x y := by simp [h, edist_comm]
  dist_triangle x y z := by
    simp only [h]
    exact ENNReal.toReal_le_add (edist_triangle _ _ _) (edist_ne_top _ _) (edist_ne_top _ _)
  edist := edist
  edist_dist _ _ := by simp only [h, ENNReal.ofReal_toReal (edist_ne_top _ _)]
  toUniformSpace := e.toUniformSpace
  uniformity_dist := e.uniformity_edist.trans <| by
    simpa only [ENNReal.coe_toNNReal (edist_ne_top _ _), h]
      using (Metric.uniformity_edist_aux fun x y : Œ± => (edist x y).toNNReal).symm
#align pseudo_emetric_space.to_pseudo_metric_space_of_dist PseudoEMetricSpace.toPseudoMetricSpaceOfDist

/-- One gets a pseudometric space from an emetric space if the edistance
is everywhere finite, by pushing the edistance to reals. We set it up so that the edist and the
uniformity are defeq in the pseudometric space and the emetric space. -/
@[reducible]
def PseudoEMetricSpace.toPseudoMetricSpace {Œ± : Type u} [PseudoEMetricSpace Œ±]
    (h : ‚àÄ x y : Œ±, edist x y ‚â† ‚ä§) : PseudoMetricSpace Œ± :=
  PseudoEMetricSpace.toPseudoMetricSpaceOfDist (fun x y => ENNReal.toReal (edist x y)) h fun _ _ =>
    rfl
#align pseudo_emetric_space.to_pseudo_metric_space PseudoEMetricSpace.toPseudoMetricSpace

/-- Build a new pseudometric space from an old one where the bundled bornology structure is provably
(but typically non-definitionaly) equal to some given bornology structure.
See Note [forgetful inheritance].
-/
@[reducible]
def PseudoMetricSpace.replaceBornology {Œ±} [B : Bornology Œ±] (m : PseudoMetricSpace Œ±)
    (H : ‚àÄ s, @IsBounded _ B s ‚Üî @IsBounded _ PseudoMetricSpace.toBornology s) :
    PseudoMetricSpace Œ± :=
  { m with
    toBornology := B
    cobounded_sets := Set.ext <| compl_surjective.forall.2 fun s =>
        (H s).trans <| by rw [isBounded_iff, mem_setOf_eq, compl_compl] }
#align pseudo_metric_space.replace_bornology PseudoMetricSpace.replaceBornology

theorem PseudoMetricSpace.replaceBornology_eq {Œ±} [m : PseudoMetricSpace Œ±] [B : Bornology Œ±]
    (H : ‚àÄ s, @IsBounded _ B s ‚Üî @IsBounded _ PseudoMetricSpace.toBornology s) :
    PseudoMetricSpace.replaceBornology _ H = m := by
  ext
  rfl
#align pseudo_metric_space.replace_bornology_eq PseudoMetricSpace.replaceBornology_eq

-- ensure that the uniformity is unchanged when replacing the bornology.
example {Œ±} [B : Bornology Œ±] (m : PseudoMetricSpace Œ±)
    (H : ‚àÄ s, @IsBounded _ B s ‚Üî @IsBounded _ PseudoMetricSpace.toBornology s) :
  (PseudoMetricSpace.replaceBornology m H).toUniformSpace = m.toUniformSpace := rfl

/-- A very useful criterion to show that a space is complete is to show that all sequences
which satisfy a bound of the form `dist (u n) (u m) < B N` for all `n m ‚â• N` are
converging. This is often applied for `B N = 2^{-N}`, i.e., with a very fast convergence to
`0`, which makes it possible to use arguments of converging series, while this is impossible
to do in general for arbitrary Cauchy sequences. -/
theorem Metric.complete_of_convergent_controlled_sequences (B : ‚Ñï ‚Üí Real) (hB : ‚àÄ n, 0 < B n)
    (H : ‚àÄ u : ‚Ñï ‚Üí Œ±, (‚àÄ N n m : ‚Ñï, N ‚â§ n ‚Üí N ‚â§ m ‚Üí dist (u n) (u m) < B N) ‚Üí
      ‚àÉ x, Tendsto u atTop (ùìù x)) :
    CompleteSpace Œ± :=
  UniformSpace.complete_of_convergent_controlled_sequences
    (fun n => { p : Œ± √ó Œ± | dist p.1 p.2 < B n }) (fun n => dist_mem_uniformity <| hB n) H
#align metric.complete_of_convergent_controlled_sequences Metric.complete_of_convergent_controlled_sequences

theorem Metric.complete_of_cauchySeq_tendsto :
    (‚àÄ u : ‚Ñï ‚Üí Œ±, CauchySeq u ‚Üí ‚àÉ a, Tendsto u atTop (ùìù a)) ‚Üí CompleteSpace Œ± :=
  EMetric.complete_of_cauchySeq_tendsto
#align metric.complete_of_cauchy_seq_tendsto Metric.complete_of_cauchySeq_tendsto

section Real

/-- Instantiate the reals as a pseudometric space. -/
instance Real.pseudoMetricSpace : PseudoMetricSpace ‚Ñù where
  dist x y := |x - y|
  dist_self := by simp [abs_zero]
  dist_comm x y := abs_sub_comm _ _
  dist_triangle x y z := abs_sub_le _ _ _
  edist_dist := fun x y => by exact ENNReal.coe_nnreal_eq _
#align real.pseudo_metric_space Real.pseudoMetricSpace

theorem Real.dist_eq (x y : ‚Ñù) : dist x y = |x - y| := rfl
#align real.dist_eq Real.dist_eq

theorem Real.nndist_eq (x y : ‚Ñù) : nndist x y = Real.nnabs (x - y) := rfl
#align real.nndist_eq Real.nndist_eq

theorem Real.nndist_eq' (x y : ‚Ñù) : nndist x y = Real.nnabs (y - x) :=
  nndist_comm _ _
#align real.nndist_eq' Real.nndist_eq'

theorem Real.dist_0_eq_abs (x : ‚Ñù) : dist x 0 = |x| := by simp [Real.dist_eq]
#align real.dist_0_eq_abs Real.dist_0_eq_abs

theorem Real.dist_left_le_of_mem_uIcc {x y z : ‚Ñù} (h : y ‚àà uIcc x z) : dist x y ‚â§ dist x z := by
  simpa only [dist_comm x] using abs_sub_left_of_mem_uIcc h
#align real.dist_left_le_of_mem_uIcc Real.dist_left_le_of_mem_uIcc

theorem Real.dist_right_le_of_mem_uIcc {x y z : ‚Ñù} (h : y ‚àà uIcc x z) : dist y z ‚â§ dist x z := by
  simpa only [dist_comm _ z] using abs_sub_right_of_mem_uIcc h
#align real.dist_right_le_of_mem_uIcc Real.dist_right_le_of_mem_uIcc

theorem Real.dist_le_of_mem_uIcc {x y x' y' : ‚Ñù} (hx : x ‚àà uIcc x' y') (hy : y ‚àà uIcc x' y') :
    dist x y ‚â§ dist x' y' :=
  abs_sub_le_of_uIcc_subset_uIcc <| uIcc_subset_uIcc (by rwa [uIcc_comm]) (by rwa [uIcc_comm])
#align real.dist_le_of_mem_uIcc Real.dist_le_of_mem_uIcc

theorem Real.dist_le_of_mem_Icc {x y x' y' : ‚Ñù} (hx : x ‚àà Icc x' y') (hy : y ‚àà Icc x' y') :
    dist x y ‚â§ y' - x' := by
  simpa only [Real.dist_eq, abs_of_nonpos (sub_nonpos.2 <| hx.1.trans hx.2), neg_sub] using
    Real.dist_le_of_mem_uIcc (Icc_subset_uIcc hx) (Icc_subset_uIcc hy)
#align real.dist_le_of_mem_Icc Real.dist_le_of_mem_Icc

theorem Real.dist_le_of_mem_Icc_01 {x y : ‚Ñù} (hx : x ‚àà Icc (0 : ‚Ñù) 1) (hy : y ‚àà Icc (0 : ‚Ñù) 1) :
    dist x y ‚â§ 1 := by simpa only [sub_zero] using Real.dist_le_of_mem_Icc hx hy
#align real.dist_le_of_mem_Icc_01 Real.dist_le_of_mem_Icc_01

instance : OrderTopology ‚Ñù :=
  orderTopology_of_nhds_abs fun x => by
    simp only [nhds_basis_ball.eq_biInf, ball, Real.dist_eq, abs_sub_comm]

theorem Real.ball_eq_Ioo (x r : ‚Ñù) : ball x r = Ioo (x - r) (x + r) :=
  Set.ext fun y => by
    rw [mem_ball, dist_comm, Real.dist_eq, abs_sub_lt_iff, mem_Ioo, ‚Üê sub_lt_iff_lt_add',
      sub_lt_comm]
#align real.ball_eq_Ioo Real.ball_eq_Ioo

theorem Real.closedBall_eq_Icc {x r : ‚Ñù} : closedBall x r = Icc (x - r) (x + r) := by
  ext y
  rw [mem_closedBall, dist_comm, Real.dist_eq, abs_sub_le_iff, mem_Icc, ‚Üê sub_le_iff_le_add',
    sub_le_comm]
#align real.closed_ball_eq_Icc Real.closedBall_eq_Icc

theorem Real.Ioo_eq_ball (x y : ‚Ñù) : Ioo x y = ball ((x + y) / 2) ((y - x) / 2) := by
  rw [Real.ball_eq_Ioo, ‚Üê sub_div, add_comm, ‚Üê sub_add, add_sub_cancel', add_self_div_two,
    ‚Üê add_div, add_assoc, add_sub_cancel'_right, add_self_div_two]
#align real.Ioo_eq_ball Real.Ioo_eq_ball

theorem Real.Icc_eq_closedBall (x y : ‚Ñù) : Icc x y = closedBall ((x + y) / 2) ((y - x) / 2) := by
  rw [Real.closedBall_eq_Icc, ‚Üê sub_div, add_comm, ‚Üê sub_add, add_sub_cancel', add_self_div_two, ‚Üê
    add_div, add_assoc, add_sub_cancel'_right, add_self_div_two]
#align real.Icc_eq_closed_ball Real.Icc_eq_closedBall

section MetricOrdered

variable [Preorder Œ±] [CompactIccSpace Œ±]

theorem totallyBounded_Icc (a b : Œ±) : TotallyBounded (Icc a b) :=
  isCompact_Icc.totallyBounded
#align totally_bounded_Icc totallyBounded_Icc

theorem totallyBounded_Ico (a b : Œ±) : TotallyBounded (Ico a b) :=
  totallyBounded_subset Ico_subset_Icc_self (totallyBounded_Icc a b)
#align totally_bounded_Ico totallyBounded_Ico

theorem totallyBounded_Ioc (a b : Œ±) : TotallyBounded (Ioc a b) :=
  totallyBounded_subset Ioc_subset_Icc_self (totallyBounded_Icc a b)
#align totally_bounded_Ioc totallyBounded_Ioc

theorem totallyBounded_Ioo (a b : Œ±) : TotallyBounded (Ioo a b) :=
  totallyBounded_subset Ioo_subset_Icc_self (totallyBounded_Icc a b)
#align totally_bounded_Ioo totallyBounded_Ioo

end MetricOrdered

/-- Special case of the sandwich theorem; see `tendsto_of_tendsto_of_tendsto_of_le_of_le'` for the
general case. -/
theorem squeeze_zero' {Œ±} {f g : Œ± ‚Üí ‚Ñù} {t‚ÇÄ : Filter Œ±} (hf : ‚àÄ·∂† t in t‚ÇÄ, 0 ‚â§ f t)
    (hft : ‚àÄ·∂† t in t‚ÇÄ, f t ‚â§ g t) (g0 : Tendsto g t‚ÇÄ (nhds 0)) : Tendsto f t‚ÇÄ (ùìù 0) :=
  tendsto_of_tendsto_of_tendsto_of_le_of_le' tendsto_const_nhds g0 hf hft
#align squeeze_zero' squeeze_zero'

/-- Special case of the sandwich theorem; see `tendsto_of_tendsto_of_tendsto_of_le_of_le`
and `tendsto_of_tendsto_of_tendsto_of_le_of_le'` for the general case. -/
theorem squeeze_zero {Œ±} {f g : Œ± ‚Üí ‚Ñù} {t‚ÇÄ : Filter Œ±} (hf : ‚àÄ t, 0 ‚â§ f t) (hft : ‚àÄ t, f t ‚â§ g t)
    (g0 : Tendsto g t‚ÇÄ (ùìù 0)) : Tendsto f t‚ÇÄ (ùìù 0) :=
  squeeze_zero' (eventually_of_forall hf) (eventually_of_forall hft) g0
#align squeeze_zero squeeze_zero

theorem Metric.uniformity_eq_comap_nhds_zero :
    ùì§ Œ± = comap (fun p : Œ± √ó Œ± => dist p.1 p.2) (ùìù (0 : ‚Ñù)) := by
  ext s
  simp only [mem_uniformity_dist, (nhds_basis_ball.comap _).mem_iff]
  simp [subset_def, Real.dist_0_eq_abs]
#align metric.uniformity_eq_comap_nhds_zero Metric.uniformity_eq_comap_nhds_zero

theorem cauchySeq_iff_tendsto_dist_atTop_0 [Nonempty Œ≤] [SemilatticeSup Œ≤] {u : Œ≤ ‚Üí Œ±} :
    CauchySeq u ‚Üî Tendsto (fun n : Œ≤ √ó Œ≤ => dist (u n.1) (u n.2)) atTop (ùìù 0) := by
  rw [cauchySeq_iff_tendsto, Metric.uniformity_eq_comap_nhds_zero, tendsto_comap_iff]; rfl
#align cauchy_seq_iff_tendsto_dist_at_top_0 cauchySeq_iff_tendsto_dist_atTop_0

theorem tendsto_uniformity_iff_dist_tendsto_zero {f : Œπ ‚Üí Œ± √ó Œ±} {p : Filter Œπ} :
    Tendsto f p (ùì§ Œ±) ‚Üî Tendsto (fun x => dist (f x).1 (f x).2) p (ùìù 0) := by
  rw [Metric.uniformity_eq_comap_nhds_zero, tendsto_comap_iff]; rfl
#align tendsto_uniformity_iff_dist_tendsto_zero tendsto_uniformity_iff_dist_tendsto_zero

theorem Filter.Tendsto.congr_dist {f‚ÇÅ f‚ÇÇ : Œπ ‚Üí Œ±} {p : Filter Œπ} {a : Œ±}
    (h‚ÇÅ : Tendsto f‚ÇÅ p (ùìù a)) (h : Tendsto (fun x => dist (f‚ÇÅ x) (f‚ÇÇ x)) p (ùìù 0)) :
    Tendsto f‚ÇÇ p (ùìù a) :=
  h‚ÇÅ.congr_uniformity <| tendsto_uniformity_iff_dist_tendsto_zero.2 h
#align filter.tendsto.congr_dist Filter.Tendsto.congr_dist

alias tendsto_of_tendsto_of_dist := Filter.Tendsto.congr_dist
#align tendsto_of_tendsto_of_dist tendsto_of_tendsto_of_dist

theorem tendsto_iff_of_dist {f‚ÇÅ f‚ÇÇ : Œπ ‚Üí Œ±} {p : Filter Œπ} {a : Œ±}
    (h : Tendsto (fun x => dist (f‚ÇÅ x) (f‚ÇÇ x)) p (ùìù 0)) : Tendsto f‚ÇÅ p (ùìù a) ‚Üî Tendsto f‚ÇÇ p (ùìù a) :=
  Uniform.tendsto_congr <| tendsto_uniformity_iff_dist_tendsto_zero.2 h
#align tendsto_iff_of_dist tendsto_iff_of_dist

/-- If `u` is a neighborhood of `x`, then for small enough `r`, the closed ball
`Metric.closedBall x r` is contained in `u`. -/
theorem eventually_closedBall_subset {x : Œ±} {u : Set Œ±} (hu : u ‚àà ùìù x) :
    ‚àÄ·∂† r in ùìù (0 : ‚Ñù), closedBall x r ‚äÜ u := by
  obtain ‚ü®Œµ, Œµpos, hŒµ‚ü© : ‚àÉ Œµ, 0 < Œµ ‚àß closedBall x Œµ ‚äÜ u := nhds_basis_closedBall.mem_iff.1 hu
  have : Iic Œµ ‚àà ùìù (0 : ‚Ñù) := Iic_mem_nhds Œµpos
  filter_upwards [this] with _ hr using Subset.trans (closedBall_subset_closedBall hr) hŒµ
#align eventually_closed_ball_subset eventually_closedBall_subset

end Real

section CauchySeq

variable [Nonempty Œ≤] [SemilatticeSup Œ≤]

/-- In a pseudometric space, Cauchy sequences are characterized by the fact that, eventually,
the distance between its elements is arbitrarily small -/
-- porting note: @[nolint ge_or_gt] doesn't exist
theorem Metric.cauchySeq_iff {u : Œ≤ ‚Üí Œ±} :
    CauchySeq u ‚Üî ‚àÄ Œµ > 0, ‚àÉ N, ‚àÄ m ‚â• N, ‚àÄ n ‚â• N, dist (u m) (u n) < Œµ :=
  uniformity_basis_dist.cauchySeq_iff
#align metric.cauchy_seq_iff Metric.cauchySeq_iff

/-- A variation around the pseudometric characterization of Cauchy sequences -/
theorem Metric.cauchySeq_iff' {u : Œ≤ ‚Üí Œ±} :
    CauchySeq u ‚Üî ‚àÄ Œµ > 0, ‚àÉ N, ‚àÄ n ‚â• N, dist (u n) (u N) < Œµ :=
  uniformity_basis_dist.cauchySeq_iff'
#align metric.cauchy_seq_iff' Metric.cauchySeq_iff'

-- see Note [nolint_ge]
/-- In a pseudometric space, uniform Cauchy sequences are characterized by the fact that,
eventually, the distance between all its elements is uniformly, arbitrarily small -/
-- porting note: no attr @[nolint ge_or_gt]
theorem Metric.uniformCauchySeqOn_iff {Œ≥ : Type*} {F : Œ≤ ‚Üí Œ≥ ‚Üí Œ±} {s : Set Œ≥} :
    UniformCauchySeqOn F atTop s ‚Üî ‚àÄ Œµ > (0 : ‚Ñù),
      ‚àÉ N : Œ≤, ‚àÄ m ‚â• N, ‚àÄ n ‚â• N, ‚àÄ x ‚àà s, dist (F m x) (F n x) < Œµ := by
  constructor
  ¬∑ intro h Œµ hŒµ
    let u := { a : Œ± √ó Œ± | dist a.fst a.snd < Œµ }
    have hu : u ‚àà ùì§ Œ± := Metric.mem_uniformity_dist.mpr ‚ü®Œµ, hŒµ, by simp‚ü©
    rw [‚Üê @Filter.eventually_atTop_prod_self' _ _ _ fun m =>
      ‚àÄ x ‚àà s, dist (F m.fst x) (F m.snd x) < Œµ]
    specialize h u hu
    rw [prod_atTop_atTop_eq] at h
    exact h.mono fun n h x hx => h x hx
  ¬∑ intro h u hu
    rcases Metric.mem_uniformity_dist.mp hu with ‚ü®Œµ, hŒµ, hab‚ü©
    rcases h Œµ hŒµ with ‚ü®N, hN‚ü©
    rw [prod_atTop_atTop_eq, eventually_atTop]
    use (N, N)
    intro b hb x hx
    rcases hb with ‚ü®hbl, hbr‚ü©
    exact hab (hN b.fst hbl.ge b.snd hbr.ge x hx)
#align metric.uniform_cauchy_seq_on_iff Metric.uniformCauchySeqOn_iff

/-- If the distance between `s n` and `s m`, `n ‚â§ m` is bounded above by `b n`
and `b` converges to zero, then `s` is a Cauchy sequence.  -/
theorem cauchySeq_of_le_tendsto_0' {s : Œ≤ ‚Üí Œ±} (b : Œ≤ ‚Üí ‚Ñù)
    (h : ‚àÄ n m : Œ≤, n ‚â§ m ‚Üí dist (s n) (s m) ‚â§ b n) (h‚ÇÄ : Tendsto b atTop (ùìù 0)) : CauchySeq s :=
  Metric.cauchySeq_iff'.2 fun Œµ Œµ0 => (h‚ÇÄ.eventually (gt_mem_nhds Œµ0)).exists.imp fun N hN n hn =>
    calc dist (s n) (s N) = dist (s N) (s n) := dist_comm _ _
    _ ‚â§ b N := h _ _ hn
    _ < Œµ := hN
#align cauchy_seq_of_le_tendsto_0' cauchySeq_of_le_tendsto_0'

/-- If the distance between `s n` and `s m`, `n, m ‚â• N` is bounded above by `b N`
and `b` converges to zero, then `s` is a Cauchy sequence.  -/
theorem cauchySeq_of_le_tendsto_0 {s : Œ≤ ‚Üí Œ±} (b : Œ≤ ‚Üí ‚Ñù)
    (h : ‚àÄ n m N : Œ≤, N ‚â§ n ‚Üí N ‚â§ m ‚Üí dist (s n) (s m) ‚â§ b N) (h‚ÇÄ : Tendsto b atTop (ùìù 0)) :
    CauchySeq s :=
  cauchySeq_of_le_tendsto_0' b (fun _n _m hnm => h _ _ _ le_rfl hnm) h‚ÇÄ
#align cauchy_seq_of_le_tendsto_0 cauchySeq_of_le_tendsto_0

/-- A Cauchy sequence on the natural numbers is bounded. -/
theorem cauchySeq_bdd {u : ‚Ñï ‚Üí Œ±} (hu : CauchySeq u) : ‚àÉ R > 0, ‚àÄ m n, dist (u m) (u n) < R := by
  rcases Metric.cauchySeq_iff'.1 hu 1 zero_lt_one with ‚ü®N, hN‚ü©
  suffices : ‚àÉ R > 0, ‚àÄ n, dist (u n) (u N) < R
  ¬∑ rcases this with ‚ü®R, R0, H‚ü©
    exact ‚ü®_, add_pos R0 R0, fun m n =>
      lt_of_le_of_lt (dist_triangle_right _ _ _) (add_lt_add (H m) (H n))‚ü©
  let R := Finset.sup (Finset.range N) fun n => nndist (u n) (u N)
  refine' ‚ü®‚ÜëR + 1, add_pos_of_nonneg_of_pos R.2 zero_lt_one, fun n => _‚ü©
  cases' le_or_lt N n with h h
  ¬∑ exact lt_of_lt_of_le (hN _ h) (le_add_of_nonneg_left R.2)
  ¬∑ have : _ ‚â§ R := Finset.le_sup (Finset.mem_range.2 h)
    exact lt_of_le_of_lt this (lt_add_of_pos_right _ zero_lt_one)
#align cauchy_seq_bdd cauchySeq_bdd

/-- Yet another metric characterization of Cauchy sequences on integers. This one is often the
most efficient. -/
theorem cauchySeq_iff_le_tendsto_0 {s : ‚Ñï ‚Üí Œ±} :
    CauchySeq s ‚Üî
      ‚àÉ b : ‚Ñï ‚Üí ‚Ñù,
        (‚àÄ n, 0 ‚â§ b n) ‚àß
          (‚àÄ n m N : ‚Ñï, N ‚â§ n ‚Üí N ‚â§ m ‚Üí dist (s n) (s m) ‚â§ b N) ‚àß Tendsto b atTop (ùìù 0) :=
  ‚ü®fun hs => by
    /- `s` is a Cauchy sequence. The sequence `b` will be constructed by taking
      the supremum of the distances between `s n` and `s m` for `n m ‚â• N`.
      First, we prove that all these distances are bounded, as otherwise the Sup
      would not make sense. -/
    let S N := (fun p : ‚Ñï √ó ‚Ñï => dist (s p.1) (s p.2)) '' { p | p.1 ‚â• N ‚àß p.2 ‚â• N }
    have hS : ‚àÄ N, ‚àÉ x, ‚àÄ y ‚àà S N, y ‚â§ x := by
      rcases cauchySeq_bdd hs with ‚ü®R, -, hR‚ü©
      refine' fun N => ‚ü®R, _‚ü©
      rintro _ ‚ü®‚ü®m, n‚ü©, _, rfl‚ü©
      exact le_of_lt (hR m n)
    -- Prove that it bounds the distances of points in the Cauchy sequence
    have ub : ‚àÄ m n N, N ‚â§ m ‚Üí N ‚â§ n ‚Üí dist (s m) (s n) ‚â§ sSup (S N) := fun m n N hm hn =>
      le_csSup (hS N) ‚ü®‚ü®_, _‚ü©, ‚ü®hm, hn‚ü©, rfl‚ü©
    have S0m : ‚àÄ n, (0 : ‚Ñù) ‚àà S n := fun n => ‚ü®‚ü®n, n‚ü©, ‚ü®le_rfl, le_rfl‚ü©, dist_self _‚ü©
    have S0 := fun n => le_csSup (hS n) (S0m n)
    -- Prove that it tends to `0`, by using the Cauchy property of `s`
    refine' ‚ü®fun N => sSup (S N), S0, ub, Metric.tendsto_atTop.2 fun Œµ Œµ0 => _‚ü©
    refine' (Metric.cauchySeq_iff.1 hs (Œµ / 2) (half_pos Œµ0)).imp fun N hN n hn => _
    rw [Real.dist_0_eq_abs, abs_of_nonneg (S0 n)]
    refine' lt_of_le_of_lt (csSup_le ‚ü®_, S0m _‚ü© _) (half_lt_self Œµ0)
    rintro _ ‚ü®‚ü®m', n'‚ü©, ‚ü®hm', hn'‚ü©, rfl‚ü©
    exact le_of_lt (hN _ (le_trans hn hm') _ (le_trans hn hn')),
   fun ‚ü®b, _, b_bound, b_lim‚ü© => cauchySeq_of_le_tendsto_0 b b_bound b_lim‚ü©
#align cauchy_seq_iff_le_tendsto_0 cauchySeq_iff_le_tendsto_0

end CauchySeq

/-- Pseudometric space structure pulled back by a function. -/
@[reducible]
def PseudoMetricSpace.induced {Œ± Œ≤} (f : Œ± ‚Üí Œ≤) (m : PseudoMetricSpace Œ≤) :
    PseudoMetricSpace Œ± where
  dist x y := dist (f x) (f y)
  dist_self x := dist_self _
  dist_comm x y := dist_comm _ _
  dist_triangle x y z := dist_triangle _ _ _
  edist x y := edist (f x) (f y)
  edist_dist x y := edist_dist _ _
  toUniformSpace := UniformSpace.comap f m.toUniformSpace
  uniformity_dist := (uniformity_basis_dist.comap _).eq_biInf
  toBornology := Bornology.induced f
  cobounded_sets := Set.ext fun s => mem_comap_iff_compl.trans <| by
    simp only [‚Üê isBounded_def, isBounded_iff, ball_image_iff, mem_setOf]
#align pseudo_metric_space.induced PseudoMetricSpace.induced

/-- Pull back a pseudometric space structure by an inducing map. This is a version of
`PseudoMetricSpace.induced` useful in case if the domain already has a `TopologicalSpace`
structure. -/
def Inducing.comapPseudoMetricSpace {Œ± Œ≤} [TopologicalSpace Œ±] [m : PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : Inducing f) : PseudoMetricSpace Œ± :=
  .replaceTopology (.induced f m) hf.induced
#align inducing.comap_pseudo_metric_space Inducing.comapPseudoMetricSpace

/-- Pull back a pseudometric space structure by a uniform inducing map. This is a version of
`PseudoMetricSpace.induced` useful in case if the domain already has a `UniformSpace`
structure. -/
def UniformInducing.comapPseudoMetricSpace {Œ± Œ≤} [UniformSpace Œ±] [m : PseudoMetricSpace Œ≤]
    (f : Œ± ‚Üí Œ≤) (h : UniformInducing f) : PseudoMetricSpace Œ± :=
  .replaceUniformity (.induced f m) h.comap_uniformity.symm
#align uniform_inducing.comap_pseudo_metric_space UniformInducing.comapPseudoMetricSpace

instance Subtype.pseudoMetricSpace {p : Œ± ‚Üí Prop} : PseudoMetricSpace (Subtype p) :=
  PseudoMetricSpace.induced Subtype.val ‚Äπ_‚Ä∫
#align subtype.pseudo_metric_space Subtype.pseudoMetricSpace

theorem Subtype.dist_eq {p : Œ± ‚Üí Prop} (x y : Subtype p) : dist x y = dist (x : Œ±) y :=
  rfl
#align subtype.dist_eq Subtype.dist_eq

theorem Subtype.nndist_eq {p : Œ± ‚Üí Prop} (x y : Subtype p) : nndist x y = nndist (x : Œ±) y :=
  rfl
#align subtype.nndist_eq Subtype.nndist_eq

namespace MulOpposite

@[to_additive]
instance instPseudoMetricSpaceMulOpposite : PseudoMetricSpace Œ±·µê·µí·µñ :=
  PseudoMetricSpace.induced MulOpposite.unop ‚Äπ_‚Ä∫

@[to_additive (attr := simp)]
theorem dist_unop (x y : Œ±·µê·µí·µñ) : dist (unop x) (unop y) = dist x y := rfl
#align mul_opposite.dist_unop MulOpposite.dist_unop
#align add_opposite.dist_unop AddOpposite.dist_unop

@[to_additive (attr := simp)]
theorem dist_op (x y : Œ±) : dist (op x) (op y) = dist x y := rfl
#align mul_opposite.dist_op MulOpposite.dist_op
#align add_opposite.dist_op AddOpposite.dist_op

@[to_additive (attr := simp)]
theorem nndist_unop (x y : Œ±·µê·µí·µñ) : nndist (unop x) (unop y) = nndist x y := rfl
#align mul_opposite.nndist_unop MulOpposite.nndist_unop
#align add_opposite.nndist_unop AddOpposite.nndist_unop

@[to_additive (attr := simp)]
theorem nndist_op (x y : Œ±) : nndist (op x) (op y) = nndist x y := rfl
#align mul_opposite.nndist_op MulOpposite.nndist_op
#align add_opposite.nndist_op AddOpposite.nndist_op

end MulOpposite

section NNReal

instance : PseudoMetricSpace ‚Ñù‚â•0 := Subtype.pseudoMetricSpace

theorem NNReal.dist_eq (a b : ‚Ñù‚â•0) : dist a b = |(a : ‚Ñù) - b| := rfl
#align nnreal.dist_eq NNReal.dist_eq

theorem NNReal.nndist_eq (a b : ‚Ñù‚â•0) : nndist a b = max (a - b) (b - a) :=
  eq_of_forall_ge_iff fun _ => by
    simp only [‚Üê NNReal.coe_le_coe, coe_nndist, dist_eq, max_le_iff, abs_sub_le_iff,
      tsub_le_iff_right, NNReal.coe_add]
#align nnreal.nndist_eq NNReal.nndist_eq

@[simp]
theorem NNReal.nndist_zero_eq_val (z : ‚Ñù‚â•0) : nndist 0 z = z := by
  simp only [NNReal.nndist_eq, max_eq_right, tsub_zero, zero_tsub, zero_le']
#align nnreal.nndist_zero_eq_val NNReal.nndist_zero_eq_val

@[simp]
theorem NNReal.nndist_zero_eq_val' (z : ‚Ñù‚â•0) : nndist z 0 = z := by
  rw [nndist_comm]
  exact NNReal.nndist_zero_eq_val z
#align nnreal.nndist_zero_eq_val' NNReal.nndist_zero_eq_val'

theorem NNReal.le_add_nndist (a b : ‚Ñù‚â•0) : a ‚â§ b + nndist a b := by
  suffices (a : ‚Ñù) ‚â§ (b : ‚Ñù) + dist a b by
    rwa [‚Üê NNReal.coe_le_coe, NNReal.coe_add, coe_nndist]
  rw [‚Üê sub_le_iff_le_add']
  exact le_of_abs_le (dist_eq a b).ge
#align nnreal.le_add_nndist NNReal.le_add_nndist

end NNReal

section ULift

variable [PseudoMetricSpace Œ≤]

instance : PseudoMetricSpace (ULift Œ≤) :=
  PseudoMetricSpace.induced ULift.down ‚Äπ_‚Ä∫

theorem ULift.dist_eq (x y : ULift Œ≤) : dist x y = dist x.down y.down := rfl
#align ulift.dist_eq ULift.dist_eq

theorem ULift.nndist_eq (x y : ULift Œ≤) : nndist x y = nndist x.down y.down := rfl
#align ulift.nndist_eq ULift.nndist_eq

@[simp]
theorem ULift.dist_up_up (x y : Œ≤) : dist (ULift.up x) (ULift.up y) = dist x y := rfl
#align ulift.dist_up_up ULift.dist_up_up

@[simp]
theorem ULift.nndist_up_up (x y : Œ≤) : nndist (ULift.up x) (ULift.up y) = nndist x y := rfl
#align ulift.nndist_up_up ULift.nndist_up_up

end ULift

section Prod

variable [PseudoMetricSpace Œ≤]

-- porting note: added `let`, otherwise `simp` failed
instance Prod.pseudoMetricSpaceMax : PseudoMetricSpace (Œ± √ó Œ≤) :=
  let i := PseudoEMetricSpace.toPseudoMetricSpaceOfDist
    (fun x y : Œ± √ó Œ≤ => dist x.1 y.1 ‚äî dist x.2 y.2)
    (fun x y => (max_lt (edist_lt_top _ _) (edist_lt_top _ _)).ne) fun x y => by
      simp only [sup_eq_max, dist_edist, ‚Üê ENNReal.toReal_max (edist_ne_top _ _) (edist_ne_top _ _),
        Prod.edist_eq]
  i.replaceBornology fun s => by
    simp only [‚Üê isBounded_image_fst_and_snd, isBounded_iff_eventually, ball_image_iff, ‚Üê
      eventually_and, ‚Üê forall_and, ‚Üê max_le_iff]
    rfl
#align prod.pseudo_metric_space_max Prod.pseudoMetricSpaceMax

theorem Prod.dist_eq {x y : Œ± √ó Œ≤} : dist x y = max (dist x.1 y.1) (dist x.2 y.2) := rfl
#align prod.dist_eq Prod.dist_eq

@[simp]
theorem dist_prod_same_left {x : Œ±} {y‚ÇÅ y‚ÇÇ : Œ≤} : dist (x, y‚ÇÅ) (x, y‚ÇÇ) = dist y‚ÇÅ y‚ÇÇ := by
  simp [Prod.dist_eq, dist_nonneg]
#align dist_prod_same_left dist_prod_same_left

@[simp]
theorem dist_prod_same_right {x‚ÇÅ x‚ÇÇ : Œ±} {y : Œ≤} : dist (x‚ÇÅ, y) (x‚ÇÇ, y) = dist x‚ÇÅ x‚ÇÇ := by
  simp [Prod.dist_eq, dist_nonneg]
#align dist_prod_same_right dist_prod_same_right

theorem ball_prod_same (x : Œ±) (y : Œ≤) (r : ‚Ñù) : ball x r √óÀ¢ ball y r = ball (x, y) r :=
  ext fun z => by simp [Prod.dist_eq]
#align ball_prod_same ball_prod_same

theorem closedBall_prod_same (x : Œ±) (y : Œ≤) (r : ‚Ñù) :
    closedBall x r √óÀ¢ closedBall y r = closedBall (x, y) r :=
  ext fun z => by simp [Prod.dist_eq]
#align closed_ball_prod_same closedBall_prod_same

theorem sphere_prod (x : Œ± √ó Œ≤) (r : ‚Ñù) :
    sphere x r = sphere x.1 r √óÀ¢ closedBall x.2 r ‚à™ closedBall x.1 r √óÀ¢ sphere x.2 r := by
  obtain hr | rfl | hr := lt_trichotomy r 0
  ¬∑ simp [hr]
  ¬∑ cases x
    simp_rw [‚Üê closedBall_eq_sphere_of_nonpos le_rfl, union_self, closedBall_prod_same]
  ¬∑ ext ‚ü®x', y'‚ü©
    simp_rw [Set.mem_union, Set.mem_prod, Metric.mem_closedBall, Metric.mem_sphere, Prod.dist_eq,
      max_eq_iff]
    refine' or_congr (and_congr_right _) (and_comm.trans (and_congr_left _))
    all_goals rintro rfl; rfl
#align sphere_prod sphere_prod

end Prod

-- porting note: 3 new lemmas
theorem dist_dist_dist_le_left (x y z : Œ±) : dist (dist x z) (dist y z) ‚â§ dist x y :=
  abs_dist_sub_le ..

theorem dist_dist_dist_le_right (x y z : Œ±) : dist (dist x y) (dist x z) ‚â§ dist y z := by
  simpa only [dist_comm x] using dist_dist_dist_le_left y z x

theorem dist_dist_dist_le (x y x' y' : Œ±) : dist (dist x y) (dist x' y') ‚â§ dist x x' + dist y y' :=
  (dist_triangle _ _ _).trans <|
    add_le_add (dist_dist_dist_le_left _ _ _) (dist_dist_dist_le_right _ _ _)

theorem uniformContinuous_dist : UniformContinuous fun p : Œ± √ó Œ± => dist p.1 p.2 :=
  Metric.uniformContinuous_iff.2 fun Œµ Œµ0 =>
    ‚ü®Œµ / 2, half_pos Œµ0, fun {a b} h =>
      calc dist (dist a.1 a.2) (dist b.1 b.2) ‚â§ dist a.1 b.1 + dist a.2 b.2 :=
        dist_dist_dist_le _ _ _ _
      _ ‚â§ dist a b + dist a b := add_le_add (le_max_left _ _) (le_max_right _ _)
      _ < Œµ / 2 + Œµ / 2 := add_lt_add h h
      _ = Œµ := add_halves Œµ‚ü©
#align uniform_continuous_dist uniformContinuous_dist

protected theorem UniformContinuous.dist [UniformSpace Œ≤] {f g : Œ≤ ‚Üí Œ±} (hf : UniformContinuous f)
    (hg : UniformContinuous g) : UniformContinuous fun b => dist (f b) (g b) :=
  uniformContinuous_dist.comp (hf.prod_mk hg)
#align uniform_continuous.dist UniformContinuous.dist

@[continuity]
theorem continuous_dist : Continuous fun p : Œ± √ó Œ± => dist p.1 p.2 :=
  uniformContinuous_dist.continuous
#align continuous_dist continuous_dist

@[continuity]
protected theorem Continuous.dist [TopologicalSpace Œ≤] {f g : Œ≤ ‚Üí Œ±} (hf : Continuous f)
    (hg : Continuous g) : Continuous fun b => dist (f b) (g b) :=
  continuous_dist.comp (hf.prod_mk hg : _)
#align continuous.dist Continuous.dist

protected theorem Filter.Tendsto.dist {f g : Œ≤ ‚Üí Œ±} {x : Filter Œ≤} {a b : Œ±}
    (hf : Tendsto f x (ùìù a)) (hg : Tendsto g x (ùìù b)) :
    Tendsto (fun x => dist (f x) (g x)) x (ùìù (dist a b)) :=
  (continuous_dist.tendsto (a, b)).comp (hf.prod_mk_nhds hg)
#align filter.tendsto.dist Filter.Tendsto.dist

theorem nhds_comap_dist (a : Œ±) : ((ùìù (0 : ‚Ñù)).comap (dist ¬∑ a)) = ùìù a := by
  simp only [@nhds_eq_comap_uniformity Œ±, Metric.uniformity_eq_comap_nhds_zero, comap_comap,
    (¬∑ ‚àò ¬∑), dist_comm]
#align nhds_comap_dist nhds_comap_dist

theorem tendsto_iff_dist_tendsto_zero {f : Œ≤ ‚Üí Œ±} {x : Filter Œ≤} {a : Œ±} :
    Tendsto f x (ùìù a) ‚Üî Tendsto (fun b => dist (f b) a) x (ùìù 0) := by
  rw [‚Üê nhds_comap_dist a, tendsto_comap_iff]; rfl
#align tendsto_iff_dist_tendsto_zero tendsto_iff_dist_tendsto_zero

theorem continuous_iff_continuous_dist [TopologicalSpace Œ≤] {f : Œ≤ ‚Üí Œ±} :
    Continuous f ‚Üî Continuous fun x : Œ≤ √ó Œ≤ => dist (f x.1) (f x.2) :=
  ‚ü®fun h => h.fst'.dist h.snd', fun h =>
    continuous_iff_continuousAt.2 fun _ => tendsto_iff_dist_tendsto_zero.2 <|
      (h.comp (continuous_id.prod_mk continuous_const)).tendsto' _ _ <| dist_self _‚ü©
#align continuous_iff_continuous_dist continuous_iff_continuous_dist

theorem uniformContinuous_nndist : UniformContinuous fun p : Œ± √ó Œ± => nndist p.1 p.2 :=
  uniformContinuous_dist.subtype_mk _
#align uniform_continuous_nndist uniformContinuous_nndist

protected theorem UniformContinuous.nndist [UniformSpace Œ≤] {f g : Œ≤ ‚Üí Œ±} (hf : UniformContinuous f)
    (hg : UniformContinuous g) : UniformContinuous fun b => nndist (f b) (g b) :=
  uniformContinuous_nndist.comp (hf.prod_mk hg)
#align uniform_continuous.nndist UniformContinuous.nndist

theorem continuous_nndist : Continuous fun p : Œ± √ó Œ± => nndist p.1 p.2 :=
  uniformContinuous_nndist.continuous
#align continuous_nndist continuous_nndist

protected theorem Continuous.nndist [TopologicalSpace Œ≤] {f g : Œ≤ ‚Üí Œ±} (hf : Continuous f)
    (hg : Continuous g) : Continuous fun b => nndist (f b) (g b) :=
  continuous_nndist.comp (hf.prod_mk hg : _)
#align continuous.nndist Continuous.nndist

protected theorem Filter.Tendsto.nndist {f g : Œ≤ ‚Üí Œ±} {x : Filter Œ≤} {a b : Œ±}
    (hf : Tendsto f x (ùìù a)) (hg : Tendsto g x (ùìù b)) :
    Tendsto (fun x => nndist (f x) (g x)) x (ùìù (nndist a b)) :=
  (continuous_nndist.tendsto (a, b)).comp (hf.prod_mk_nhds hg)
#align filter.tendsto.nndist Filter.Tendsto.nndist

namespace Metric

variable {x y z : Œ±} {Œµ Œµ‚ÇÅ Œµ‚ÇÇ : ‚Ñù} {s : Set Œ±}

theorem isClosed_ball : IsClosed (closedBall x Œµ) :=
  isClosed_le (continuous_id.dist continuous_const) continuous_const
#align metric.is_closed_ball Metric.isClosed_ball

theorem isClosed_sphere : IsClosed (sphere x Œµ) :=
  isClosed_eq (continuous_id.dist continuous_const) continuous_const
#align metric.is_closed_sphere Metric.isClosed_sphere

@[simp]
theorem closure_closedBall : closure (closedBall x Œµ) = closedBall x Œµ :=
  isClosed_ball.closure_eq
#align metric.closure_closed_ball Metric.closure_closedBall

@[simp]
theorem closure_sphere : closure (sphere x Œµ) = sphere x Œµ :=
  isClosed_sphere.closure_eq
#align metric.closure_sphere Metric.closure_sphere

theorem closure_ball_subset_closedBall : closure (ball x Œµ) ‚äÜ closedBall x Œµ :=
  closure_minimal ball_subset_closedBall isClosed_ball
#align metric.closure_ball_subset_closed_ball Metric.closure_ball_subset_closedBall

theorem frontier_ball_subset_sphere : frontier (ball x Œµ) ‚äÜ sphere x Œµ :=
  frontier_lt_subset_eq (continuous_id.dist continuous_const) continuous_const
#align metric.frontier_ball_subset_sphere Metric.frontier_ball_subset_sphere

theorem frontier_closedBall_subset_sphere : frontier (closedBall x Œµ) ‚äÜ sphere x Œµ :=
  frontier_le_subset_eq (continuous_id.dist continuous_const) continuous_const
#align metric.frontier_closed_ball_subset_sphere Metric.frontier_closedBall_subset_sphere

theorem ball_subset_interior_closedBall : ball x Œµ ‚äÜ interior (closedBall x Œµ) :=
  interior_maximal ball_subset_closedBall isOpen_ball
#align metric.ball_subset_interior_closed_ball Metric.ball_subset_interior_closedBall

/-- Œµ-characterization of the closure in pseudometric spaces-/
theorem mem_closure_iff {s : Set Œ±} {a : Œ±} : a ‚àà closure s ‚Üî ‚àÄ Œµ > 0, ‚àÉ b ‚àà s, dist a b < Œµ :=
  (mem_closure_iff_nhds_basis nhds_basis_ball).trans <| by simp only [mem_ball, dist_comm]
#align metric.mem_closure_iff Metric.mem_closure_iff

theorem mem_closure_range_iff {e : Œ≤ ‚Üí Œ±} {a : Œ±} :
    a ‚àà closure (range e) ‚Üî ‚àÄ Œµ > 0, ‚àÉ k : Œ≤, dist a (e k) < Œµ := by
  simp only [mem_closure_iff, exists_range_iff]
#align metric.mem_closure_range_iff Metric.mem_closure_range_iff

theorem mem_closure_range_iff_nat {e : Œ≤ ‚Üí Œ±} {a : Œ±} :
    a ‚àà closure (range e) ‚Üî ‚àÄ n : ‚Ñï, ‚àÉ k : Œ≤, dist a (e k) < 1 / ((n : ‚Ñù) + 1) :=
  (mem_closure_iff_nhds_basis nhds_basis_ball_inv_nat_succ).trans <| by
    simp only [mem_ball, dist_comm, exists_range_iff, forall_const]
#align metric.mem_closure_range_iff_nat Metric.mem_closure_range_iff_nat

theorem mem_of_closed' {s : Set Œ±} (hs : IsClosed s) {a : Œ±} :
    a ‚àà s ‚Üî ‚àÄ Œµ > 0, ‚àÉ b ‚àà s, dist a b < Œµ := by
  simpa only [hs.closure_eq] using @mem_closure_iff _ _ s a
#align metric.mem_of_closed' Metric.mem_of_closed'

theorem closedBall_zero' (x : Œ±) : closedBall x 0 = closure {x} :=
  Subset.antisymm
    (fun _y hy =>
      mem_closure_iff.2 fun _Œµ Œµ0 => ‚ü®x, mem_singleton x, (mem_closedBall.1 hy).trans_lt Œµ0‚ü©)
    (closure_minimal (singleton_subset_iff.2 (dist_self x).le) isClosed_ball)
#align metric.closed_ball_zero' Metric.closedBall_zero'

lemma eventually_isCompact_closedBall [LocallyCompactSpace Œ±] (x : Œ±) :
    ‚àÄ·∂† r in ùìù (0 : ‚Ñù), IsCompact (closedBall x r) := by
  rcases local_compact_nhds (x := x) (n := univ) univ_mem with ‚ü®s, hs, -, s_compact‚ü©
  filter_upwards [eventually_closedBall_subset hs] with r hr
  exact IsCompact.of_isClosed_subset s_compact isClosed_ball hr

lemma exists_isCompact_closedBall [LocallyCompactSpace Œ±] (x : Œ±) :
    ‚àÉ r, 0 < r ‚àß IsCompact (closedBall x r) := by
  have : ‚àÄ·∂† r in ùìù[>] 0, IsCompact (closedBall x r) :=
    eventually_nhdsWithin_of_eventually_nhds (eventually_isCompact_closedBall x)
  simpa only [and_comm] using (this.and self_mem_nhdsWithin).exists

theorem dense_iff {s : Set Œ±} : Dense s ‚Üî ‚àÄ x, ‚àÄ r > 0, (ball x r ‚à© s).Nonempty :=
  forall_congr' fun x => by
    simp only [mem_closure_iff, Set.Nonempty, exists_prop, mem_inter_iff, mem_ball', and_comm]
#align metric.dense_iff Metric.dense_iff

theorem denseRange_iff {f : Œ≤ ‚Üí Œ±} : DenseRange f ‚Üî ‚àÄ x, ‚àÄ r > 0, ‚àÉ y, dist x (f y) < r :=
  forall_congr' fun x => by simp only [mem_closure_iff, exists_range_iff]
#align metric.dense_range_iff Metric.denseRange_iff

-- porting note: `TopologicalSpace.IsSeparable.separableSpace` moved to `EMetricSpace`

/-- The preimage of a separable set by an inducing map is separable. -/
protected theorem _root_.Inducing.isSeparable_preimage {f : Œ≤ ‚Üí Œ±} [TopologicalSpace Œ≤]
    (hf : Inducing f) {s : Set Œ±} (hs : IsSeparable s) : IsSeparable (f ‚Åª¬π' s) := by
  have : SeparableSpace s := hs.separableSpace
  have : SecondCountableTopology s := UniformSpace.secondCountable_of_separable _
  have : Inducing ((mapsTo_preimage f s).restrict _ _ _) :=
    (hf.comp inducing_subtype_val).codRestrict _
  have := this.secondCountableTopology
  exact isSeparable_of_separableSpace_subtype _
#align inducing.is_separable_preimage Inducing.isSeparable_preimage

protected theorem _root_.Embedding.isSeparable_preimage {f : Œ≤ ‚Üí Œ±} [TopologicalSpace Œ≤]
    (hf : Embedding f) {s : Set Œ±} (hs : IsSeparable s) : IsSeparable (f ‚Åª¬π' s) :=
  hf.toInducing.isSeparable_preimage hs
#align embedding.is_separable_preimage Embedding.isSeparable_preimage

/-- If a map is continuous on a separable set `s`, then the image of `s` is also separable. -/
theorem _root_.ContinuousOn.isSeparable_image [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ±}
    (hf : ContinuousOn f s) (hs : IsSeparable s) : IsSeparable (f '' s) := by
  rw [image_eq_range, ‚Üê image_univ]
  exact (isSeparable_univ_iff.2 hs.separableSpace).image hf.restrict
#align continuous_on.is_separable_image ContinuousOn.isSeparable_image

end Metric

section Pi

open Finset

variable {œÄ : Œ≤ ‚Üí Type*} [Fintype Œ≤] [‚àÄ b, PseudoMetricSpace (œÄ b)]

/-- A finite product of pseudometric spaces is a pseudometric space, with the sup distance. -/
instance pseudoMetricSpacePi : PseudoMetricSpace (‚àÄ b, œÄ b) := by
  /- we construct the instance from the pseudoemetric space instance to avoid checking again that
    the uniformity is the same as the product uniformity, but we register nevertheless a nice
    formula for the distance -/
  let i := PseudoEMetricSpace.toPseudoMetricSpaceOfDist
    (fun f g : ‚àÄ b, œÄ b => ((sup univ fun b => nndist (f b) (g b) : ‚Ñù‚â•0) : ‚Ñù))
    (fun f g => ((Finset.sup_lt_iff bot_lt_top).2 fun b _ => edist_lt_top _ _).ne)
    (fun f g => by
      simp only [edist_pi_def, edist_nndist, ‚Üê ENNReal.coe_finset_sup, ENNReal.coe_toReal])
  refine i.replaceBornology fun s => ?_
  simp only [‚Üê isBounded_def, isBounded_iff_eventually, ‚Üê forall_isBounded_image_eval_iff,
    ball_image_iff, ‚Üê Filter.eventually_all, Function.eval_apply, @dist_nndist (œÄ _)]
  refine' eventually_congr ((eventually_ge_atTop 0).mono fun C hC => _)
  lift C to ‚Ñù‚â•0 using hC
  refine' ‚ü®fun H x hx y hy => NNReal.coe_le_coe.2 <| Finset.sup_le fun b _ => H b x hx y hy,
    fun H b x hx y hy => NNReal.coe_le_coe.2 _‚ü©
  simpa only using Finset.sup_le_iff.1 (NNReal.coe_le_coe.1 <| H hx hy) b (Finset.mem_univ b)
#align pseudo_metric_space_pi pseudoMetricSpacePi

theorem nndist_pi_def (f g : ‚àÄ b, œÄ b) : nndist f g = sup univ fun b => nndist (f b) (g b) :=
  NNReal.eq rfl
#align nndist_pi_def nndist_pi_def

theorem dist_pi_def (f g : ‚àÄ b, œÄ b) : dist f g = (sup univ fun b => nndist (f b) (g b) : ‚Ñù‚â•0) :=
  rfl
#align dist_pi_def dist_pi_def

theorem nndist_pi_le_iff {f g : ‚àÄ b, œÄ b} {r : ‚Ñù‚â•0} :
    nndist f g ‚â§ r ‚Üî ‚àÄ b, nndist (f b) (g b) ‚â§ r := by simp [nndist_pi_def]
#align nndist_pi_le_iff nndist_pi_le_iff

theorem nndist_pi_lt_iff {f g : ‚àÄ b, œÄ b} {r : ‚Ñù‚â•0} (hr : 0 < r) :
    nndist f g < r ‚Üî ‚àÄ b, nndist (f b) (g b) < r := by
  simp [nndist_pi_def, Finset.sup_lt_iff (show ‚ä• < r from hr)]
#align nndist_pi_lt_iff nndist_pi_lt_iff

theorem nndist_pi_eq_iff {f g : ‚àÄ b, œÄ b} {r : ‚Ñù‚â•0} (hr : 0 < r) :
    nndist f g = r ‚Üî (‚àÉ i, nndist (f i) (g i) = r) ‚àß ‚àÄ b, nndist (f b) (g b) ‚â§ r := by
  rw [eq_iff_le_not_lt, nndist_pi_lt_iff hr, nndist_pi_le_iff, not_forall, and_comm]
  simp_rw [not_lt, and_congr_left_iff, le_antisymm_iff]
  intro h
  refine' exists_congr fun b => _
  apply (and_iff_right <| h _).symm
#align nndist_pi_eq_iff nndist_pi_eq_iff

theorem dist_pi_lt_iff {f g : ‚àÄ b, œÄ b} {r : ‚Ñù} (hr : 0 < r) :
    dist f g < r ‚Üî ‚àÄ b, dist (f b) (g b) < r := by
  lift r to ‚Ñù‚â•0 using hr.le
  exact nndist_pi_lt_iff hr
#align dist_pi_lt_iff dist_pi_lt_iff

theorem dist_pi_le_iff {f g : ‚àÄ b, œÄ b} {r : ‚Ñù} (hr : 0 ‚â§ r) :
    dist f g ‚â§ r ‚Üî ‚àÄ b, dist (f b) (g b) ‚â§ r := by
  lift r to ‚Ñù‚â•0 using hr
  exact nndist_pi_le_iff
#align dist_pi_le_iff dist_pi_le_iff

theorem dist_pi_eq_iff {f g : ‚àÄ b, œÄ b} {r : ‚Ñù} (hr : 0 < r) :
    dist f g = r ‚Üî (‚àÉ i, dist (f i) (g i) = r) ‚àß ‚àÄ b, dist (f b) (g b) ‚â§ r := by
  lift r to ‚Ñù‚â•0 using hr.le
  simp_rw [‚Üê coe_nndist, NNReal.coe_eq, nndist_pi_eq_iff hr, NNReal.coe_le_coe]
#align dist_pi_eq_iff dist_pi_eq_iff

theorem dist_pi_le_iff' [Nonempty Œ≤] {f g : ‚àÄ b, œÄ b} {r : ‚Ñù} :
    dist f g ‚â§ r ‚Üî ‚àÄ b, dist (f b) (g b) ‚â§ r := by
  by_cases hr : 0 ‚â§ r
  ¬∑ exact dist_pi_le_iff hr
  ¬∑ exact iff_of_false (fun h => hr <| dist_nonneg.trans h) fun h =>
      hr <| dist_nonneg.trans <| h <| Classical.arbitrary _
#align dist_pi_le_iff' dist_pi_le_iff'

theorem dist_pi_const_le (a b : Œ±) : (dist (fun _ : Œ≤ => a) fun _ => b) ‚â§ dist a b :=
  (dist_pi_le_iff dist_nonneg).2 fun _ => le_rfl
#align dist_pi_const_le dist_pi_const_le

theorem nndist_pi_const_le (a b : Œ±) : (nndist (fun _ : Œ≤ => a) fun _ => b) ‚â§ nndist a b :=
  nndist_pi_le_iff.2 fun _ => le_rfl
#align nndist_pi_const_le nndist_pi_const_le

@[simp]
theorem dist_pi_const [Nonempty Œ≤] (a b : Œ±) : (dist (fun _ : Œ≤ => a) fun _ => b) = dist a b := by
  simpa only [dist_edist] using congr_arg ENNReal.toReal (edist_pi_const a b)
#align dist_pi_const dist_pi_const

@[simp]
theorem nndist_pi_const [Nonempty Œ≤] (a b : Œ±) :
    (nndist (fun _ : Œ≤ => a) fun _ => b) = nndist a b :=
  NNReal.eq <| dist_pi_const a b
#align nndist_pi_const nndist_pi_const

theorem nndist_le_pi_nndist (f g : ‚àÄ b, œÄ b) (b : Œ≤) : nndist (f b) (g b) ‚â§ nndist f g := by
  rw [‚Üê ENNReal.coe_le_coe, ‚Üê edist_nndist, ‚Üê edist_nndist]
  exact edist_le_pi_edist f g b
#align nndist_le_pi_nndist nndist_le_pi_nndist

theorem dist_le_pi_dist (f g : ‚àÄ b, œÄ b) (b : Œ≤) : dist (f b) (g b) ‚â§ dist f g := by
  simp only [dist_nndist, NNReal.coe_le_coe, nndist_le_pi_nndist f g b]
#align dist_le_pi_dist dist_le_pi_dist

/-- An open ball in a product space is a product of open balls. See also `ball_pi'`
for a version assuming `Nonempty Œ≤` instead of `0 < r`. -/
theorem ball_pi (x : ‚àÄ b, œÄ b) {r : ‚Ñù} (hr : 0 < r) :
    ball x r = Set.pi univ fun b => ball (x b) r := by
  ext p
  simp [dist_pi_lt_iff hr]
#align ball_pi ball_pi

/-- An open ball in a product space is a product of open balls. See also `ball_pi`
for a version assuming `0 < r` instead of `Nonempty Œ≤`. -/
theorem ball_pi' [Nonempty Œ≤] (x : ‚àÄ b, œÄ b) (r : ‚Ñù) :
    ball x r = Set.pi univ fun b => ball (x b) r :=
  (lt_or_le 0 r).elim (ball_pi x) fun hr => by simp [ball_eq_empty.2 hr]
#align ball_pi' ball_pi'

/-- A closed ball in a product space is a product of closed balls. See also `closedBall_pi'`
for a version assuming `Nonempty Œ≤` instead of `0 ‚â§ r`. -/
theorem closedBall_pi (x : ‚àÄ b, œÄ b) {r : ‚Ñù} (hr : 0 ‚â§ r) :
    closedBall x r = Set.pi univ fun b => closedBall (x b) r := by
  ext p
  simp [dist_pi_le_iff hr]
#align closed_ball_pi closedBall_pi

/-- A closed ball in a product space is a product of closed balls. See also `closedBall_pi`
for a version assuming `0 ‚â§ r` instead of `Nonempty Œ≤`. -/
theorem closedBall_pi' [Nonempty Œ≤] (x : ‚àÄ b, œÄ b) (r : ‚Ñù) :
    closedBall x r = Set.pi univ fun b => closedBall (x b) r :=
  (le_or_lt 0 r).elim (closedBall_pi x) fun hr => by simp [closedBall_eq_empty.2 hr]
#align closed_ball_pi' closedBall_pi'

/-- A sphere in a product space is a union of spheres on each component restricted to the closed
ball. -/
theorem sphere_pi (x : ‚àÄ b, œÄ b) {r : ‚Ñù} (h : 0 < r ‚à® Nonempty Œ≤) :
    sphere x r = (‚ãÉ i : Œ≤, Function.eval i ‚Åª¬π' sphere (x i) r) ‚à© closedBall x r := by
  obtain hr | rfl | hr := lt_trichotomy r 0
  ¬∑ simp [hr]
  ¬∑ rw [closedBall_eq_sphere_of_nonpos le_rfl, eq_comm, Set.inter_eq_right]
    letI := h.resolve_left (lt_irrefl _)
    inhabit Œ≤
    refine' subset_iUnion_of_subset default _
    intro x hx
    replace hx := hx.le
    rw [dist_pi_le_iff le_rfl] at hx
    exact le_antisymm (hx default) dist_nonneg
  ¬∑ ext
    simp [dist_pi_eq_iff hr, dist_pi_le_iff hr.le]
#align sphere_pi sphere_pi

@[simp]
theorem Fin.nndist_insertNth_insertNth {n : ‚Ñï} {Œ± : Fin (n + 1) ‚Üí Type*}
    [‚àÄ i, PseudoMetricSpace (Œ± i)] (i : Fin (n + 1)) (x y : Œ± i) (f g : ‚àÄ j, Œ± (i.succAbove j)) :
    nndist (i.insertNth x f) (i.insertNth y g) = max (nndist x y) (nndist f g) :=
  eq_of_forall_ge_iff fun c => by simp [nndist_pi_le_iff, i.forall_iff_succAbove]
#align fin.nndist_insert_nth_insert_nth Fin.nndist_insertNth_insertNth

@[simp]
theorem Fin.dist_insertNth_insertNth {n : ‚Ñï} {Œ± : Fin (n + 1) ‚Üí Type*}
    [‚àÄ i, PseudoMetricSpace (Œ± i)] (i : Fin (n + 1)) (x y : Œ± i) (f g : ‚àÄ j, Œ± (i.succAbove j)) :
    dist (i.insertNth x f) (i.insertNth y g) = max (dist x y) (dist f g) := by
  simp only [dist_nndist, Fin.nndist_insertNth_insertNth, NNReal.coe_max]
#align fin.dist_insert_nth_insert_nth Fin.dist_insertNth_insertNth

theorem Real.dist_le_of_mem_pi_Icc {x y x' y' : Œ≤ ‚Üí ‚Ñù} (hx : x ‚àà Icc x' y') (hy : y ‚àà Icc x' y') :
    dist x y ‚â§ dist x' y' := by
  refine' (dist_pi_le_iff dist_nonneg).2 fun b =>
    (Real.dist_le_of_mem_uIcc _ _).trans (dist_le_pi_dist x' y' b) <;> refine' Icc_subset_uIcc _
  exacts [‚ü®hx.1 _, hx.2 _‚ü©, ‚ü®hy.1 _, hy.2 _‚ü©]
#align real.dist_le_of_mem_pi_Icc Real.dist_le_of_mem_pi_Icc

end Pi

section Compact

/-- Any compact set in a pseudometric space can be covered by finitely many balls of a given
positive radius -/
theorem finite_cover_balls_of_compact {Œ± : Type u} [PseudoMetricSpace Œ±] {s : Set Œ±}
    (hs : IsCompact s) {e : ‚Ñù} (he : 0 < e) :
    ‚àÉ t, t ‚äÜ s ‚àß Set.Finite t ‚àß s ‚äÜ ‚ãÉ x ‚àà t, ball x e :=
  let ‚ü®t, hts, ht‚ü© := hs.elim_nhds_subcover _ (fun x _ => ball_mem_nhds x he)
  ‚ü®t, hts, t.finite_toSet, ht‚ü©
#align finite_cover_balls_of_compact finite_cover_balls_of_compact

alias IsCompact.finite_cover_balls := finite_cover_balls_of_compact
#align is_compact.finite_cover_balls IsCompact.finite_cover_balls

end Compact

section ProperSpace

open Metric

/-- A pseudometric space is proper if all closed balls are compact. -/
class ProperSpace (Œ± : Type u) [PseudoMetricSpace Œ±] : Prop where
  isCompact_closedBall : ‚àÄ x : Œ±, ‚àÄ r, IsCompact (closedBall x r)
#align proper_space ProperSpace

export ProperSpace (isCompact_closedBall)

/-- In a proper pseudometric space, all spheres are compact. -/
theorem isCompact_sphere {Œ± : Type*} [PseudoMetricSpace Œ±] [ProperSpace Œ±] (x : Œ±) (r : ‚Ñù) :
    IsCompact (sphere x r) :=
  (isCompact_closedBall x r).of_isClosed_subset isClosed_sphere sphere_subset_closedBall
#align is_compact_sphere isCompact_sphere

/-- In a proper pseudometric space, any sphere is a `CompactSpace` when considered as a subtype. -/
instance Metric.sphere.compactSpace {Œ± : Type*} [PseudoMetricSpace Œ±] [ProperSpace Œ±]
    (x : Œ±) (r : ‚Ñù) : CompactSpace (sphere x r) :=
  isCompact_iff_compactSpace.mp (isCompact_sphere _ _)

-- see Note [lower instance priority]
/-- A proper pseudo metric space is sigma compact, and therefore second countable. -/
instance (priority := 100) secondCountable_of_proper [ProperSpace Œ±] :
    SecondCountableTopology Œ± := by
  -- We already have `sigmaCompactSpace_of_locallyCompact_secondCountable`, so we don't
  -- add an instance for `SigmaCompactSpace`.
  suffices SigmaCompactSpace Œ± by exact EMetric.secondCountable_of_sigmaCompact Œ±
  rcases em (Nonempty Œ±) with (‚ü®‚ü®x‚ü©‚ü© | hn)
  ¬∑ exact ‚ü®‚ü®fun n => closedBall x n, fun n => isCompact_closedBall _ _, iUnion_closedBall_nat _‚ü©‚ü©
  ¬∑ exact ‚ü®‚ü®fun _ => ‚àÖ, fun _ => isCompact_empty, iUnion_eq_univ_iff.2 fun x => (hn ‚ü®x‚ü©).elim‚ü©‚ü©
#align second_countable_of_proper secondCountable_of_proper

theorem tendsto_dist_right_cocompact_atTop [ProperSpace Œ±] (x : Œ±) :
    Tendsto (fun y => dist y x) (cocompact Œ±) atTop :=
  (hasBasis_cocompact.tendsto_iff atTop_basis).2 fun r _ =>
    ‚ü®closedBall x r, isCompact_closedBall x r, fun _y hy => (not_le.1 <| mt mem_closedBall.2 hy).le‚ü©
#align tendsto_dist_right_cocompact_at_top tendsto_dist_right_cocompact_atTop

theorem tendsto_dist_left_cocompact_atTop [ProperSpace Œ±] (x : Œ±) :
    Tendsto (dist x) (cocompact Œ±) atTop := by
  simpa only [dist_comm] using tendsto_dist_right_cocompact_atTop x
#align tendsto_dist_left_cocompact_at_top tendsto_dist_left_cocompact_atTop

/-- If all closed balls of large enough radius are compact, then the space is proper. Especially
useful when the lower bound for the radius is 0. -/
theorem properSpace_of_compact_closedBall_of_le (R : ‚Ñù)
    (h : ‚àÄ x : Œ±, ‚àÄ r, R ‚â§ r ‚Üí IsCompact (closedBall x r)) : ProperSpace Œ± :=
  ‚ü®fun x r => IsCompact.of_isClosed_subset (h x (max r R) (le_max_right _ _)) isClosed_ball
    (closedBall_subset_closedBall <| le_max_left _ _)‚ü©
#align proper_space_of_compact_closed_ball_of_le properSpace_of_compact_closedBall_of_le

-- A compact pseudometric space is proper
-- see Note [lower instance priority]
instance (priority := 100) proper_of_compact [CompactSpace Œ±] : ProperSpace Œ± :=
  ‚ü®fun _ _ => isClosed_ball.isCompact‚ü©
#align proper_of_compact proper_of_compact

-- see Note [lower instance priority]
/-- A proper space is locally compact -/
instance (priority := 100) locally_compact_of_proper [ProperSpace Œ±] : LocallyCompactSpace Œ± :=
  locallyCompactSpace_of_hasBasis (fun _ => nhds_basis_closedBall) fun _ _ _ =>
    isCompact_closedBall _ _
#align locally_compact_of_proper locally_compact_of_proper

-- see Note [lower instance priority]
/-- A proper space is complete -/
instance (priority := 100) complete_of_proper [ProperSpace Œ±] : CompleteSpace Œ± :=
  ‚ü®fun {f} hf => by
    /- We want to show that the Cauchy filter `f` is converging. It suffices to find a closed
      ball (therefore compact by properness) where it is nontrivial. -/
    obtain ‚ü®t, t_fset, ht‚ü© : ‚àÉ t ‚àà f, ‚àÄ x ‚àà t, ‚àÄ y ‚àà t, dist x y < 1 :=
      (Metric.cauchy_iff.1 hf).2 1 zero_lt_one
    rcases hf.1.nonempty_of_mem t_fset with ‚ü®x, xt‚ü©
    have : closedBall x 1 ‚àà f := mem_of_superset t_fset fun y yt => (ht y yt x xt).le
    rcases (isCompact_iff_totallyBounded_isComplete.1 (isCompact_closedBall x 1)).2 f hf
        (le_principal_iff.2 this) with
      ‚ü®y, -, hy‚ü©
    exact ‚ü®y, hy‚ü©‚ü©
#align complete_of_proper complete_of_proper

/-- A binary product of proper spaces is proper. -/
instance prod_properSpace {Œ± : Type*} {Œ≤ : Type*} [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤]
    [ProperSpace Œ±] [ProperSpace Œ≤] : ProperSpace (Œ± √ó Œ≤) where
  isCompact_closedBall := by
    rintro ‚ü®x, y‚ü© r
    rw [‚Üê closedBall_prod_same x y]
    exact (isCompact_closedBall x r).prod (isCompact_closedBall y r)
#align prod_proper_space prod_properSpace

/-- A finite product of proper spaces is proper. -/
instance pi_properSpace {œÄ : Œ≤ ‚Üí Type*} [Fintype Œ≤] [‚àÄ b, PseudoMetricSpace (œÄ b)]
    [h : ‚àÄ b, ProperSpace (œÄ b)] : ProperSpace (‚àÄ b, œÄ b) := by
  refine' properSpace_of_compact_closedBall_of_le 0 fun x r hr => _
  rw [closedBall_pi _ hr]
  exact isCompact_univ_pi fun _ => isCompact_closedBall _ _
#align pi_proper_space pi_properSpace

variable [ProperSpace Œ±] {x : Œ±} {r : ‚Ñù} {s : Set Œ±}

/-- If a nonempty ball in a proper space includes a closed set `s`, then there exists a nonempty
ball with the same center and a strictly smaller radius that includes `s`. -/
theorem exists_pos_lt_subset_ball (hr : 0 < r) (hs : IsClosed s) (h : s ‚äÜ ball x r) :
    ‚àÉ r' ‚àà Ioo 0 r, s ‚äÜ ball x r' := by
  rcases eq_empty_or_nonempty s with (rfl | hne)
  ¬∑ exact ‚ü®r / 2, ‚ü®half_pos hr, half_lt_self hr‚ü©, empty_subset _‚ü©
  have : IsCompact s :=
    (isCompact_closedBall x r).of_isClosed_subset hs (h.trans ball_subset_closedBall)
  obtain ‚ü®y, hys, hy‚ü© : ‚àÉ y ‚àà s, s ‚äÜ closedBall x (dist y x) :=
    this.exists_forall_ge hne (continuous_id.dist continuous_const).continuousOn
  have hyr : dist y x < r := h hys
  rcases exists_between hyr with ‚ü®r', hyr', hrr'‚ü©
  exact ‚ü®r', ‚ü®dist_nonneg.trans_lt hyr', hrr'‚ü©, hy.trans <| closedBall_subset_ball hyr'‚ü©
#align exists_pos_lt_subset_ball exists_pos_lt_subset_ball

/-- If a ball in a proper space includes a closed set `s`, then there exists a ball with the same
center and a strictly smaller radius that includes `s`. -/
theorem exists_lt_subset_ball (hs : IsClosed s) (h : s ‚äÜ ball x r) : ‚àÉ r' < r, s ‚äÜ ball x r' := by
  cases' le_or_lt r 0 with hr hr
  ¬∑ rw [ball_eq_empty.2 hr, subset_empty_iff] at h
    subst s
    exact (exists_lt r).imp fun r' hr' => ‚ü®hr', empty_subset _‚ü©
  ¬∑ exact (exists_pos_lt_subset_ball hr hs h).imp fun r' hr' => ‚ü®hr'.1.2, hr'.2‚ü©
#align exists_lt_subset_ball exists_lt_subset_ball

end ProperSpace

theorem IsCompact.isSeparable {s : Set Œ±} (hs : IsCompact s) : IsSeparable s :=
  haveI : CompactSpace s := isCompact_iff_compactSpace.mp hs
  isSeparable_of_separableSpace_subtype s
#align is_compact.is_separable IsCompact.isSeparable

namespace Metric

section SecondCountable

open TopologicalSpace

/-- A pseudometric space is second countable if, for every `Œµ > 0`, there is a countable set which
is `Œµ`-dense. -/
theorem secondCountable_of_almost_dense_set
    (H : ‚àÄ Œµ > (0 : ‚Ñù), ‚àÉ s : Set Œ±, s.Countable ‚àß ‚àÄ x, ‚àÉ y ‚àà s, dist x y ‚â§ Œµ) :
    SecondCountableTopology Œ± := by
  refine' EMetric.secondCountable_of_almost_dense_set fun Œµ Œµ0 => _
  rcases ENNReal.lt_iff_exists_nnreal_btwn.1 Œµ0 with ‚ü®Œµ', Œµ'0, Œµ'Œµ‚ü©
  choose s hsc y hys hyx using H Œµ' (by exact_mod_cast Œµ'0)
  refine' ‚ü®s, hsc, iUnion‚ÇÇ_eq_univ_iff.2 fun x => ‚ü®y x, hys _, le_trans _ Œµ'Œµ.le‚ü©‚ü©
  exact_mod_cast hyx x
#align metric.second_countable_of_almost_dense_set Metric.secondCountable_of_almost_dense_set

end SecondCountable

end Metric

theorem lebesgue_number_lemma_of_metric {s : Set Œ±} {Œπ : Sort*} {c : Œπ ‚Üí Set Œ±} (hs : IsCompact s)
    (hc‚ÇÅ : ‚àÄ i, IsOpen (c i)) (hc‚ÇÇ : s ‚äÜ ‚ãÉ i, c i) : ‚àÉ Œ¥ > 0, ‚àÄ x ‚àà s, ‚àÉ i, ball x Œ¥ ‚äÜ c i :=
  let ‚ü®_n, en, hn‚ü© := lebesgue_number_lemma hs hc‚ÇÅ hc‚ÇÇ
  let ‚ü®Œ¥, Œ¥0, hŒ¥‚ü© := mem_uniformity_dist.1 en
  ‚ü®Œ¥, Œ¥0, fun x hx => let ‚ü®i, hi‚ü© := hn x hx; ‚ü®i, fun _y hy => hi (hŒ¥ (mem_ball'.mp hy))‚ü©‚ü©
#align lebesgue_number_lemma_of_metric lebesgue_number_lemma_of_metric

theorem lebesgue_number_lemma_of_metric_sUnion {s : Set Œ±} {c : Set (Set Œ±)} (hs : IsCompact s)
    (hc‚ÇÅ : ‚àÄ t ‚àà c, IsOpen t) (hc‚ÇÇ : s ‚äÜ ‚ãÉ‚ÇÄ c) : ‚àÉ Œ¥ > 0, ‚àÄ x ‚àà s, ‚àÉ t ‚àà c, ball x Œ¥ ‚äÜ t := by
  rw [sUnion_eq_iUnion] at hc‚ÇÇ; simpa using lebesgue_number_lemma_of_metric hs (by simpa) hc‚ÇÇ
#align lebesgue_number_lemma_of_metric_sUnion lebesgue_number_lemma_of_metric_sUnion

namespace Metric

#align metric.bounded Bornology.IsBounded

section Bounded

variable {x : Œ±} {s t : Set Œ±} {r : ‚Ñù}

#noalign metric.bounded_iff_is_bounded

#align metric.bounded_empty Bornology.isBounded_empty
#align metric.bounded_iff_mem_bounded Bornology.isBounded_iff_forall_mem
#align metric.bounded.mono Bornology.IsBounded.subset

/-- Closed balls are bounded -/
theorem isBounded_closedBall : IsBounded (closedBall x r) :=
  isBounded_iff.2 ‚ü®r + r, fun y hy z hz =>
    calc dist y z ‚â§ dist y x + dist z x := dist_triangle_right _ _ _
    _ ‚â§ r + r := add_le_add hy hz‚ü©
#align metric.bounded_closed_ball Metric.isBounded_closedBall

/-- Open balls are bounded -/
theorem isBounded_ball : IsBounded (ball x r) :=
  isBounded_closedBall.subset ball_subset_closedBall
#align metric.bounded_ball Metric.isBounded_ball

/-- Spheres are bounded -/
theorem isBounded_sphere : IsBounded (sphere x r) :=
  isBounded_closedBall.subset sphere_subset_closedBall
#align metric.bounded_sphere Metric.isBounded_sphere

/-- Given a point, a bounded subset is included in some ball around this point -/
theorem isBounded_iff_subset_closedBall (c : Œ±) : IsBounded s ‚Üî ‚àÉ r, s ‚äÜ closedBall c r :=
  ‚ü®fun h ‚Ü¶ (isBounded_iff.1 (h.insert c)).imp fun _r hr _x hx ‚Ü¶ hr (.inr hx) (mem_insert _ _),
    fun ‚ü®_r, hr‚ü© ‚Ü¶ isBounded_closedBall.subset hr‚ü©
#align metric.bounded_iff_subset_ball Metric.isBounded_iff_subset_closedBall

theorem _root_.Bornology.IsBounded.subset_closedBall (h : IsBounded s) (c : Œ±) :
    ‚àÉ r, s ‚äÜ closedBall c r :=
  (isBounded_iff_subset_closedBall c).1 h
#align metric.bounded.subset_ball Bornology.IsBounded.subset_closedBall

theorem _root_.Bornology.IsBounded.subset_ball_lt (h : IsBounded s) (a : ‚Ñù) (c : Œ±) :
    ‚àÉ r, a < r ‚àß s ‚äÜ ball c r :=
  let ‚ü®r, hr‚ü© := h.subset_closedBall c
  ‚ü®max r a + 1, (le_max_right _ _).trans_lt (lt_add_one _), hr.trans <| closedBall_subset_ball <|
    (le_max_left _ _).trans_lt (lt_add_one _)‚ü©

theorem _root_.Bornology.IsBounded.subset_ball (h : IsBounded s) (c : Œ±) : ‚àÉ r, s ‚äÜ ball c r :=
  (h.subset_ball_lt 0 c).imp fun _ ‚Ü¶ And.right

theorem _root_.Bornology.IsBounded.subset_closedBall_lt (h : IsBounded s) (a : ‚Ñù) (c : Œ±) :
    ‚àÉ r, a < r ‚àß s ‚äÜ closedBall c r :=
  let ‚ü®r, har, hr‚ü© := h.subset_ball_lt a c
  ‚ü®r, har, hr.trans ball_subset_closedBall‚ü©
#align metric.bounded.subset_ball_lt Bornology.IsBounded.subset_closedBall_lt

theorem isBounded_closure_of_isBounded (h : IsBounded s) : IsBounded (closure s) :=
  let ‚ü®C, h‚ü© := isBounded_iff.1 h
  isBounded_iff.2 ‚ü®C, fun _a ha _b hb => (isClosed_le' C).closure_subset <|
    map_mem_closure‚ÇÇ continuous_dist ha hb h‚ü©
#align metric.bounded_closure_of_bounded Metric.isBounded_closure_of_isBounded

protected theorem _root_.Bornology.IsBounded.closure (h : IsBounded s) : IsBounded (closure s) :=
  isBounded_closure_of_isBounded h
#align metric.bounded.closure Bornology.IsBounded.closure

@[simp]
theorem isBounded_closure_iff : IsBounded (closure s) ‚Üî IsBounded s :=
  ‚ü®fun h => h.subset subset_closure, fun h => h.closure‚ü©
#align metric.bounded_closure_iff Metric.isBounded_closure_iff

#align metric.bounded_union Bornology.isBounded_union
#align metric.bounded.union Bornology.IsBounded.union
#align metric.bounded_bUnion Bornology.isBounded_biUnion
#align metric.bounded.prod Bornology.IsBounded.prod

/-- A totally bounded set is bounded -/
theorem _root_.TotallyBounded.isBounded {s : Set Œ±} (h : TotallyBounded s) : IsBounded s :=
  -- We cover the totally bounded set by finitely many balls of radius 1,
  -- and then argue that a finite union of bounded sets is bounded
  let ‚ü®_t, fint, subs‚ü© := (totallyBounded_iff.mp h) 1 zero_lt_one
  ((isBounded_biUnion fint).2 fun _ _ => isBounded_ball).subset subs
#align totally_bounded.bounded TotallyBounded.isBounded

/-- A compact set is bounded -/
theorem _root_.IsCompact.isBounded {s : Set Œ±} (h : IsCompact s) : IsBounded s :=
  -- A compact set is totally bounded, thus bounded
  h.totallyBounded.isBounded
#align is_compact.bounded IsCompact.isBounded

#align metric.bounded_of_finite Set.Finite.isBounded
#align set.finite.bounded Set.Finite.isBounded
#align metric.bounded_singleton Bornology.isBounded_singleton

/-- Characterization of the boundedness of the range of a function -/
theorem isBounded_range_iff {f : Œ≤ ‚Üí Œ±} : IsBounded (range f) ‚Üî ‚àÉ C, ‚àÄ x y, dist (f x) (f y) ‚â§ C :=
  isBounded_iff.trans <| by simp only [forall_range_iff]
#align metric.bounded_range_iff Metric.isBounded_range_iff

theorem isBounded_image_iff {f : Œ≤ ‚Üí Œ±} {s : Set Œ≤} :
    IsBounded (f '' s) ‚Üî ‚àÉ C, ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, dist (f x) (f y) ‚â§ C :=
  isBounded_iff.trans <| by simp only [ball_image_iff]

theorem isBounded_range_of_tendsto_cofinite_uniformity {f : Œ≤ ‚Üí Œ±}
    (hf : Tendsto (Prod.map f f) (.cofinite √óÀ¢ .cofinite) (ùì§ Œ±)) : IsBounded (range f) := by
  rcases (hasBasis_cofinite.prod_self.tendsto_iff uniformity_basis_dist).1 hf 1 zero_lt_one with
    ‚ü®s, hsf, hs1‚ü©
  rw [‚Üê image_union_image_compl_eq_range]
  refine (hsf.image f).isBounded.union (isBounded_image_iff.2 ‚ü®1, fun x hx y hy ‚Ü¶ ?_‚ü©)
  exact le_of_lt (hs1 (x, y) ‚ü®hx, hy‚ü©)
#align metric.bounded_range_of_tendsto_cofinite_uniformity Metric.isBounded_range_of_tendsto_cofinite_uniformity

theorem isBounded_range_of_cauchy_map_cofinite {f : Œ≤ ‚Üí Œ±} (hf : Cauchy (map f cofinite)) :
    IsBounded (range f) :=
  isBounded_range_of_tendsto_cofinite_uniformity <| (cauchy_map_iff.1 hf).2
#align metric.bounded_range_of_cauchy_map_cofinite Metric.isBounded_range_of_cauchy_map_cofinite

theorem _root_.CauchySeq.isBounded_range {f : ‚Ñï ‚Üí Œ±} (hf : CauchySeq f) : IsBounded (range f) :=
  isBounded_range_of_cauchy_map_cofinite <| by rwa [Nat.cofinite_eq_atTop]
#align cauchy_seq.bounded_range CauchySeq.isBounded_range

theorem isBounded_range_of_tendsto_cofinite {f : Œ≤ ‚Üí Œ±} {a : Œ±} (hf : Tendsto f cofinite (ùìù a)) :
    IsBounded (range f) :=
  isBounded_range_of_tendsto_cofinite_uniformity <|
    (hf.prod_map hf).mono_right <| nhds_prod_eq.symm.trans_le (nhds_le_uniformity a)
#align metric.bounded_range_of_tendsto_cofinite Metric.isBounded_range_of_tendsto_cofinite

/-- In a compact space, all sets are bounded -/
theorem isBounded_of_compactSpace [CompactSpace Œ±] : IsBounded s :=
  isCompact_univ.isBounded.subset (subset_univ _)
#align metric.bounded_of_compact_space Metric.isBounded_of_compactSpace

theorem isBounded_range_of_tendsto (u : ‚Ñï ‚Üí Œ±) {x : Œ±} (hu : Tendsto u atTop (ùìù x)) :
    IsBounded (range u) :=
  hu.cauchySeq.isBounded_range
#align metric.bounded_range_of_tendsto Metric.isBounded_range_of_tendsto

theorem disjoint_nhds_cobounded (x : Œ±) : Disjoint (ùìù x) (cobounded Œ±) :=
  disjoint_of_disjoint_of_mem disjoint_compl_right (ball_mem_nhds _ one_pos) isBounded_ball

theorem disjoint_cobounded_nhds (x : Œ±) : Disjoint (cobounded Œ±) (ùìù x) :=
  (disjoint_nhds_cobounded x).symm

theorem disjoint_nhdsSet_cobounded {s : Set Œ±} (hs : IsCompact s) : Disjoint (ùìùÀ¢ s) (cobounded Œ±) :=
  hs.disjoint_nhdsSet_left.2 fun _ _ ‚Ü¶ disjoint_nhds_cobounded _

theorem disjoint_cobounded_nhdsSet {s : Set Œ±} (hs : IsCompact s) : Disjoint (cobounded Œ±) (ùìùÀ¢ s) :=
  (disjoint_nhdsSet_cobounded hs).symm

/-- If a function is continuous within a set `s` at every point of a compact set `k`, then it is
bounded on some open neighborhood of `k` in `s`. -/
theorem exists_isOpen_isBounded_image_inter_of_isCompact_of_forall_continuousWithinAt
    [TopologicalSpace Œ≤] {k s : Set Œ≤} {f : Œ≤ ‚Üí Œ±} (hk : IsCompact k)
    (hf : ‚àÄ x ‚àà k, ContinuousWithinAt f s x) :
    ‚àÉ t, k ‚äÜ t ‚àß IsOpen t ‚àß IsBounded (f '' (t ‚à© s)) := by
  have : Disjoint (ùìùÀ¢ k ‚äì ùìü s) (comap f (cobounded Œ±)) := by
    rw [disjoint_assoc, inf_comm, hk.disjoint_nhdsSet_left]
    exact fun x hx ‚Ü¶ disjoint_left_comm.2 <|
      tendsto_comap.disjoint (disjoint_cobounded_nhds _) (hf x hx)
  rcases ((((hasBasis_nhdsSet _).inf_principal _)).disjoint_iff ((basis_sets _).comap _)).1 this
    with ‚ü®U, ‚ü®hUo, hkU‚ü©, t, ht, hd‚ü©
  refine ‚ü®U, hkU, hUo, (isBounded_compl_iff.2 ht).subset ?_‚ü©
  rwa [image_subset_iff, preimage_compl, subset_compl_iff_disjoint_right]
#align metric.exists_is_open_bounded_image_inter_of_is_compact_of_forall_continuous_within_at Metric.exists_isOpen_isBounded_image_inter_of_isCompact_of_forall_continuousWithinAt

/-- If a function is continuous at every point of a compact set `k`, then it is bounded on
some open neighborhood of `k`. -/
theorem exists_isOpen_isBounded_image_of_isCompact_of_forall_continuousAt [TopologicalSpace Œ≤]
    {k : Set Œ≤} {f : Œ≤ ‚Üí Œ±} (hk : IsCompact k) (hf : ‚àÄ x ‚àà k, ContinuousAt f x) :
    ‚àÉ t, k ‚äÜ t ‚àß IsOpen t ‚àß IsBounded (f '' t) := by
  simp_rw [‚Üê continuousWithinAt_univ] at hf
  simpa only [inter_univ] using
    exists_isOpen_isBounded_image_inter_of_isCompact_of_forall_continuousWithinAt hk hf
#align metric.exists_is_open_bounded_image_of_is_compact_of_forall_continuous_at Metric.exists_isOpen_isBounded_image_of_isCompact_of_forall_continuousAt

/-- If a function is continuous on a set `s` containing a compact set `k`, then it is bounded on
some open neighborhood of `k` in `s`. -/
theorem exists_isOpen_isBounded_image_inter_of_isCompact_of_continuousOn [TopologicalSpace Œ≤]
    {k s : Set Œ≤} {f : Œ≤ ‚Üí Œ±} (hk : IsCompact k) (hks : k ‚äÜ s) (hf : ContinuousOn f s) :
    ‚àÉ t, k ‚äÜ t ‚àß IsOpen t ‚àß IsBounded (f '' (t ‚à© s)) :=
  exists_isOpen_isBounded_image_inter_of_isCompact_of_forall_continuousWithinAt hk fun x hx =>
    hf x (hks hx)
#align metric.exists_is_open_bounded_image_inter_of_is_compact_of_continuous_on Metric.exists_isOpen_isBounded_image_inter_of_isCompact_of_continuousOn

/-- If a function is continuous on a neighborhood of a compact set `k`, then it is bounded on
some open neighborhood of `k`. -/
theorem exists_isOpen_isBounded_image_of_isCompact_of_continuousOn [TopologicalSpace Œ≤]
    {k s : Set Œ≤} {f : Œ≤ ‚Üí Œ±} (hk : IsCompact k) (hs : IsOpen s) (hks : k ‚äÜ s)
    (hf : ContinuousOn f s) : ‚àÉ t, k ‚äÜ t ‚àß IsOpen t ‚àß IsBounded (f '' t) :=
  exists_isOpen_isBounded_image_of_isCompact_of_forall_continuousAt hk fun _x hx =>
    hf.continuousAt (hs.mem_nhds (hks hx))
#align metric.exists_is_open_bounded_image_of_is_compact_of_continuous_on Metric.exists_isOpen_isBounded_image_of_isCompact_of_continuousOn

/-- The **Heine‚ÄìBorel theorem**: In a proper space, a closed bounded set is compact. -/
theorem isCompact_of_isClosed_isBounded [ProperSpace Œ±] (hc : IsClosed s) (hb : IsBounded s) :
    IsCompact s := by
  rcases eq_empty_or_nonempty s with (rfl | ‚ü®x, -‚ü©)
  ¬∑ exact isCompact_empty
  ¬∑ rcases hb.subset_closedBall x with ‚ü®r, hr‚ü©
    exact (isCompact_closedBall x r).of_isClosed_subset hc hr
#align metric.is_compact_of_is_closed_bounded Metric.isCompact_of_isClosed_isBounded

/-- The **Heine‚ÄìBorel theorem**: In a proper space, the closure of a bounded set is compact. -/
theorem _root_.Bornology.IsBounded.isCompact_closure [ProperSpace Œ±] (h : IsBounded s) :
    IsCompact (closure s) :=
  isCompact_of_isClosed_isBounded isClosed_closure h.closure
#align metric.bounded.is_compact_closure Bornology.IsBounded.isCompact_closure

-- porting note: todo: assume `[MetricSpace Œ±]` instead of `[PseudoMetricSpace Œ±] [T2Space Œ±]`
/-- The **Heine‚ÄìBorel theorem**:
In a proper Hausdorff space, a set is compact if and only if it is closed and bounded. -/
theorem isCompact_iff_isClosed_bounded [T2Space Œ±] [ProperSpace Œ±] :
    IsCompact s ‚Üî IsClosed s ‚àß IsBounded s :=
  ‚ü®fun h => ‚ü®h.isClosed, h.isBounded‚ü©, fun h => isCompact_of_isClosed_isBounded h.1 h.2‚ü©
#align metric.is_compact_iff_is_closed_bounded Metric.isCompact_iff_isClosed_bounded

theorem compactSpace_iff_isBounded_univ [ProperSpace Œ±] :
    CompactSpace Œ± ‚Üî IsBounded (univ : Set Œ±) :=
  ‚ü®@isBounded_of_compactSpace Œ± _ _, fun hb => ‚ü®isCompact_of_isClosed_isBounded isClosed_univ hb‚ü©‚ü©
#align metric.compact_space_iff_bounded_univ Metric.compactSpace_iff_isBounded_univ

section ConditionallyCompleteLinearOrder

variable [Preorder Œ±] [CompactIccSpace Œ±]

theorem isBounded_Icc (a b : Œ±) : IsBounded (Icc a b) :=
  (totallyBounded_Icc a b).isBounded
#align metric.bounded_Icc Metric.isBounded_Icc

theorem isBounded_Ico (a b : Œ±) : IsBounded (Ico a b) :=
  (totallyBounded_Ico a b).isBounded
#align metric.bounded_Ico Metric.isBounded_Ico

theorem isBounded_Ioc (a b : Œ±) : IsBounded (Ioc a b) :=
  (totallyBounded_Ioc a b).isBounded
#align metric.bounded_Ioc Metric.isBounded_Ioc

theorem isBounded_Ioo (a b : Œ±) : IsBounded (Ioo a b) :=
  (totallyBounded_Ioo a b).isBounded
#align metric.bounded_Ioo Metric.isBounded_Ioo

/-- In a pseudo metric space with a conditionally complete linear order such that the order and the
    metric structure give the same topology, any order-bounded set is metric-bounded. -/
theorem isBounded_of_bddAbove_of_bddBelow {s : Set Œ±} (h‚ÇÅ : BddAbove s) (h‚ÇÇ : BddBelow s) :
    IsBounded s :=
  let ‚ü®u, hu‚ü© := h‚ÇÅ
  let ‚ü®l, hl‚ü© := h‚ÇÇ
  (isBounded_Icc l u).subset (fun _x hx => mem_Icc.mpr ‚ü®hl hx, hu hx‚ü©)
#align metric.bounded_of_bdd_above_of_bdd_below Metric.isBounded_of_bddAbove_of_bddBelow

end ConditionallyCompleteLinearOrder

end Bounded

section Diam

variable {s : Set Œ±} {x y z : Œ±}

/-- The diameter of a set in a metric space. To get controllable behavior even when the diameter
should be infinite, we express it in terms of the emetric.diameter -/
noncomputable def diam (s : Set Œ±) : ‚Ñù :=
  ENNReal.toReal (EMetric.diam s)
#align metric.diam Metric.diam

/-- The diameter of a set is always nonnegative -/
theorem diam_nonneg : 0 ‚â§ diam s :=
  ENNReal.toReal_nonneg
#align metric.diam_nonneg Metric.diam_nonneg

theorem diam_subsingleton (hs : s.Subsingleton) : diam s = 0 := by
  simp only [diam, EMetric.diam_subsingleton hs, ENNReal.zero_toReal]
#align metric.diam_subsingleton Metric.diam_subsingleton

/-- The empty set has zero diameter -/
@[simp]
theorem diam_empty : diam (‚àÖ : Set Œ±) = 0 :=
  diam_subsingleton subsingleton_empty
#align metric.diam_empty Metric.diam_empty

/-- A singleton has zero diameter -/
@[simp]
theorem diam_singleton : diam ({x} : Set Œ±) = 0 :=
  diam_subsingleton subsingleton_singleton
#align metric.diam_singleton Metric.diam_singleton

-- Does not work as a simp-lemma, since {x, y} reduces to (insert y {x})
theorem diam_pair : diam ({x, y} : Set Œ±) = dist x y := by
  simp only [diam, EMetric.diam_pair, dist_edist]
#align metric.diam_pair Metric.diam_pair

-- Does not work as a simp-lemma, since {x, y, z} reduces to (insert z (insert y {x}))
theorem diam_triple :
    Metric.diam ({x, y, z} : Set Œ±) = max (max (dist x y) (dist x z)) (dist y z) := by
  simp only [Metric.diam, EMetric.diam_triple, dist_edist]
  rw [ENNReal.toReal_max, ENNReal.toReal_max] <;> apply_rules [ne_of_lt, edist_lt_top, max_lt]
#align metric.diam_triple Metric.diam_triple

/-- If the distance between any two points in a set is bounded by some constant `C`,
then `ENNReal.ofReal C` bounds the emetric diameter of this set. -/
theorem ediam_le_of_forall_dist_le {C : ‚Ñù} (h : ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, dist x y ‚â§ C) :
    EMetric.diam s ‚â§ ENNReal.ofReal C :=
  EMetric.diam_le fun x hx y hy => (edist_dist x y).symm ‚ñ∏ ENNReal.ofReal_le_ofReal (h x hx y hy)
#align metric.ediam_le_of_forall_dist_le Metric.ediam_le_of_forall_dist_le

/-- If the distance between any two points in a set is bounded by some non-negative constant,
this constant bounds the diameter. -/
theorem diam_le_of_forall_dist_le {C : ‚Ñù} (h‚ÇÄ : 0 ‚â§ C) (h : ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, dist x y ‚â§ C) :
    diam s ‚â§ C :=
  ENNReal.toReal_le_of_le_ofReal h‚ÇÄ (ediam_le_of_forall_dist_le h)
#align metric.diam_le_of_forall_dist_le Metric.diam_le_of_forall_dist_le

/-- If the distance between any two points in a nonempty set is bounded by some constant,
this constant bounds the diameter. -/
theorem diam_le_of_forall_dist_le_of_nonempty (hs : s.Nonempty) {C : ‚Ñù}
    (h : ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, dist x y ‚â§ C) : diam s ‚â§ C :=
  have h‚ÇÄ : 0 ‚â§ C :=
    let ‚ü®x, hx‚ü© := hs
    le_trans dist_nonneg (h x hx x hx)
  diam_le_of_forall_dist_le h‚ÇÄ h
#align metric.diam_le_of_forall_dist_le_of_nonempty Metric.diam_le_of_forall_dist_le_of_nonempty

/-- The distance between two points in a set is controlled by the diameter of the set. -/
theorem dist_le_diam_of_mem' (h : EMetric.diam s ‚â† ‚ä§) (hx : x ‚àà s) (hy : y ‚àà s) :
    dist x y ‚â§ diam s := by
  rw [diam, dist_edist]
  rw [ENNReal.toReal_le_toReal (edist_ne_top _ _) h]
  exact EMetric.edist_le_diam_of_mem hx hy
#align metric.dist_le_diam_of_mem' Metric.dist_le_diam_of_mem'

/-- Characterize the boundedness of a set in terms of the finiteness of its emetric.diameter. -/
theorem isBounded_iff_ediam_ne_top : IsBounded s ‚Üî EMetric.diam s ‚â† ‚ä§ :=
  isBounded_iff.trans <| Iff.intro
    (fun ‚ü®_C, hC‚ü© => ne_top_of_le_ne_top ENNReal.ofReal_ne_top <| ediam_le_of_forall_dist_le hC)
    fun h => ‚ü®diam s, fun _x hx _y hy => dist_le_diam_of_mem' h hx hy‚ü©
#align metric.bounded_iff_ediam_ne_top Metric.isBounded_iff_ediam_ne_top

alias ‚ü®_root_.Bornology.IsBounded.ediam_ne_top, _‚ü© := isBounded_iff_ediam_ne_top
#align metric.bounded.ediam_ne_top Bornology.IsBounded.ediam_ne_top

theorem ediam_eq_top_iff_unbounded : EMetric.diam s = ‚ä§ ‚Üî ¬¨IsBounded s :=
  isBounded_iff_ediam_ne_top.not_left.symm

theorem ediam_univ_eq_top_iff_noncompact [ProperSpace Œ±] :
    EMetric.diam (univ : Set Œ±) = ‚àû ‚Üî NoncompactSpace Œ± := by
  rw [‚Üê not_compactSpace_iff, compactSpace_iff_isBounded_univ, isBounded_iff_ediam_ne_top,
    Classical.not_not]
#align metric.ediam_univ_eq_top_iff_noncompact Metric.ediam_univ_eq_top_iff_noncompact

@[simp]
theorem ediam_univ_of_noncompact [ProperSpace Œ±] [NoncompactSpace Œ±] :
    EMetric.diam (univ : Set Œ±) = ‚àû :=
  ediam_univ_eq_top_iff_noncompact.mpr ‚Äπ_‚Ä∫
#align metric.ediam_univ_of_noncompact Metric.ediam_univ_of_noncompact

@[simp]
theorem diam_univ_of_noncompact [ProperSpace Œ±] [NoncompactSpace Œ±] : diam (univ : Set Œ±) = 0 := by
  simp [diam]
#align metric.diam_univ_of_noncompact Metric.diam_univ_of_noncompact

/-- The distance between two points in a set is controlled by the diameter of the set. -/
theorem dist_le_diam_of_mem (h : IsBounded s) (hx : x ‚àà s) (hy : y ‚àà s) : dist x y ‚â§ diam s :=
  dist_le_diam_of_mem' h.ediam_ne_top hx hy
#align metric.dist_le_diam_of_mem Metric.dist_le_diam_of_mem

theorem ediam_of_unbounded (h : ¬¨IsBounded s) : EMetric.diam s = ‚àû := ediam_eq_top_iff_unbounded.2 h
#align metric.ediam_of_unbounded Metric.ediam_of_unbounded

/-- An unbounded set has zero diameter. If you would prefer to get the value ‚àû, use `EMetric.diam`.
This lemma makes it possible to avoid side conditions in some situations -/
theorem diam_eq_zero_of_unbounded (h : ¬¨IsBounded s) : diam s = 0 := by
  rw [diam, ediam_of_unbounded h, ENNReal.top_toReal]
#align metric.diam_eq_zero_of_unbounded Metric.diam_eq_zero_of_unbounded

/-- If `s ‚äÜ t`, then the diameter of `s` is bounded by that of `t`, provided `t` is bounded. -/
theorem diam_mono {s t : Set Œ±} (h : s ‚äÜ t) (ht : IsBounded t) : diam s ‚â§ diam t :=
  ENNReal.toReal_mono ht.ediam_ne_top <| EMetric.diam_mono h
#align metric.diam_mono Metric.diam_mono

/-- The diameter of a union is controlled by the sum of the diameters, and the distance between
any two points in each of the sets. This lemma is true without any side condition, since it is
obviously true if `s ‚à™ t` is unbounded. -/
theorem diam_union {t : Set Œ±} (xs : x ‚àà s) (yt : y ‚àà t) :
    diam (s ‚à™ t) ‚â§ diam s + dist x y + diam t := by
  simp only [diam, dist_edist]
  refine (ENNReal.toReal_le_add' (EMetric.diam_union xs yt) ?_ ?_).trans
    (add_le_add_right ENNReal.toReal_add_le _)
  ¬∑ simp only [ENNReal.add_eq_top, edist_ne_top, or_false]
    exact fun h ‚Ü¶ top_unique <| h ‚ñ∏ EMetric.diam_mono (subset_union_left _ _)
  ¬∑ exact fun h ‚Ü¶ top_unique <| h ‚ñ∏ EMetric.diam_mono (subset_union_right _ _)
#align metric.diam_union Metric.diam_union

/-- If two sets intersect, the diameter of the union is bounded by the sum of the diameters. -/
theorem diam_union' {t : Set Œ±} (h : (s ‚à© t).Nonempty) : diam (s ‚à™ t) ‚â§ diam s + diam t := by
  rcases h with ‚ü®x, ‚ü®xs, xt‚ü©‚ü©
  simpa using diam_union xs xt
#align metric.diam_union' Metric.diam_union'

theorem diam_le_of_subset_closedBall {r : ‚Ñù} (hr : 0 ‚â§ r) (h : s ‚äÜ closedBall x r) :
    diam s ‚â§ 2 * r :=
  diam_le_of_forall_dist_le (mul_nonneg zero_le_two hr) fun a ha b hb =>
    calc
      dist a b ‚â§ dist a x + dist b x := dist_triangle_right _ _ _
      _ ‚â§ r + r := (add_le_add (h ha) (h hb))
      _ = 2 * r := by simp [mul_two, mul_comm]
#align metric.diam_le_of_subset_closed_ball Metric.diam_le_of_subset_closedBall

/-- The diameter of a closed ball of radius `r` is at most `2 r`. -/
theorem diam_closedBall {r : ‚Ñù} (h : 0 ‚â§ r) : diam (closedBall x r) ‚â§ 2 * r :=
  diam_le_of_subset_closedBall h Subset.rfl
#align metric.diam_closed_ball Metric.diam_closedBall

/-- The diameter of a ball of radius `r` is at most `2 r`. -/
theorem diam_ball {r : ‚Ñù} (h : 0 ‚â§ r) : diam (ball x r) ‚â§ 2 * r :=
  diam_le_of_subset_closedBall h ball_subset_closedBall
#align metric.diam_ball Metric.diam_ball

/-- If a family of complete sets with diameter tending to `0` is such that each finite intersection
is nonempty, then the total intersection is also nonempty. -/
theorem _root_.IsComplete.nonempty_iInter_of_nonempty_biInter {s : ‚Ñï ‚Üí Set Œ±}
    (h0 : IsComplete (s 0)) (hs : ‚àÄ n, IsClosed (s n)) (h's : ‚àÄ n, IsBounded (s n))
    (h : ‚àÄ N, (‚ãÇ n ‚â§ N, s n).Nonempty) (h' : Tendsto (fun n => diam (s n)) atTop (ùìù 0)) :
    (‚ãÇ n, s n).Nonempty := by
  let u N := (h N).some
  have I : ‚àÄ n N, n ‚â§ N ‚Üí u N ‚àà s n := by
    intro n N hn
    apply mem_of_subset_of_mem _ (h N).choose_spec
    intro x hx
    simp only [mem_iInter] at hx
    exact hx n hn
  have : CauchySeq u := by
    apply cauchySeq_of_le_tendsto_0 _ _ h'
    intro m n N hm hn
    exact dist_le_diam_of_mem (h's N) (I _ _ hm) (I _ _ hn)
  obtain ‚ü®x, -, xlim‚ü© : ‚àÉ x ‚àà s 0, Tendsto (fun n : ‚Ñï => u n) atTop (ùìù x) :=
    cauchySeq_tendsto_of_isComplete h0 (fun n => I 0 n (zero_le _)) this
  refine' ‚ü®x, mem_iInter.2 fun n => _‚ü©
  apply (hs n).mem_of_tendsto xlim
  filter_upwards [Ici_mem_atTop n]with p hp
  exact I n p hp
#align is_complete.nonempty_Inter_of_nonempty_bInter IsComplete.nonempty_iInter_of_nonempty_biInter

/-- In a complete space, if a family of closed sets with diameter tending to `0` is such that each
finite intersection is nonempty, then the total intersection is also nonempty. -/
theorem nonempty_iInter_of_nonempty_biInter [CompleteSpace Œ±] {s : ‚Ñï ‚Üí Set Œ±}
    (hs : ‚àÄ n, IsClosed (s n)) (h's : ‚àÄ n, IsBounded (s n)) (h : ‚àÄ N, (‚ãÇ n ‚â§ N, s n).Nonempty)
    (h' : Tendsto (fun n => diam (s n)) atTop (ùìù 0)) : (‚ãÇ n, s n).Nonempty :=
  (hs 0).isComplete.nonempty_iInter_of_nonempty_biInter hs h's h h'
#align metric.nonempty_Inter_of_nonempty_bInter Metric.nonempty_iInter_of_nonempty_biInter

end Diam

theorem exists_isLocalMin_mem_ball [ProperSpace Œ±] [TopologicalSpace Œ≤]
    [ConditionallyCompleteLinearOrder Œ≤] [OrderTopology Œ≤] {f : Œ± ‚Üí Œ≤} {a z : Œ±} {r : ‚Ñù}
    (hf : ContinuousOn f (closedBall a r)) (hz : z ‚àà closedBall a r)
    (hf1 : ‚àÄ z' ‚àà sphere a r, f z < f z') : ‚àÉ z ‚àà ball a r, IsLocalMin f z := by
  simp_rw [‚Üê closedBall_diff_ball] at hf1
  exact (isCompact_closedBall a r).exists_isLocalMin_mem_open ball_subset_closedBall hf hz hf1
    isOpen_ball
#align metric.exists_local_min_mem_ball Metric.exists_isLocalMin_mem_ball

end Metric

namespace Mathlib.Meta.Positivity

open Lean Meta Qq Function

/-- Extension for the `positivity` tactic: the diameter of a set is always nonnegative. -/
@[positivity Metric.diam _]
def evalDiam : PositivityExt where eval {_ _} _zŒ± _pŒ± e := do
  let .app _ s ‚Üê whnfR e | throwError "not Metric.diam"
  let p ‚Üê mkAppOptM ``Metric.diam_nonneg #[none, none, s]
  pure (.nonnegative p)

end Mathlib.Meta.Positivity

theorem comap_dist_right_atTop_le_cocompact (x : Œ±) :
    comap (fun y => dist y x) atTop ‚â§ cocompact Œ± := by
  refine' Filter.hasBasis_cocompact.ge_iff.2 fun s hs => mem_comap.2 _
  rcases hs.isBounded.subset_closedBall x with ‚ü®r, hr‚ü©
  exact ‚ü®Ioi r, Ioi_mem_atTop r, fun y hy hys => (mem_closedBall.1 <| hr hys).not_lt hy‚ü©
#align comap_dist_right_at_top_le_cocompact comap_dist_right_atTop_le_cocompact

theorem comap_dist_left_atTop_le_cocompact (x : Œ±) : comap (dist x) atTop ‚â§ cocompact Œ± := by
  simpa only [dist_comm _ x] using comap_dist_right_atTop_le_cocompact x
#align comap_dist_left_at_top_le_cocompact comap_dist_left_atTop_le_cocompact

theorem comap_dist_right_atTop_eq_cocompact [ProperSpace Œ±] (x : Œ±) :
    comap (fun y => dist y x) atTop = cocompact Œ± :=
  (comap_dist_right_atTop_le_cocompact x).antisymm <|
    (tendsto_dist_right_cocompact_atTop x).le_comap
#align comap_dist_right_at_top_eq_cocompact comap_dist_right_atTop_eq_cocompact

theorem comap_dist_left_atTop_eq_cocompact [ProperSpace Œ±] (x : Œ±) :
    comap (dist x) atTop = cocompact Œ± :=
  (comap_dist_left_atTop_le_cocompact x).antisymm <| (tendsto_dist_left_cocompact_atTop x).le_comap
#align comap_dist_left_at_top_eq_cocompact comap_dist_left_atTop_eq_cocompact

theorem tendsto_cocompact_of_tendsto_dist_comp_atTop {f : Œ≤ ‚Üí Œ±} {l : Filter Œ≤} (x : Œ±)
    (h : Tendsto (fun y => dist (f y) x) l atTop) : Tendsto f l (cocompact Œ±) := by
  refine' Tendsto.mono_right _ (comap_dist_right_atTop_le_cocompact x)
  rwa [tendsto_comap_iff]
#align tendsto_cocompact_of_tendsto_dist_comp_at_top tendsto_cocompact_of_tendsto_dist_comp_atTop

/-- We now define `MetricSpace`, extending `PseudoMetricSpace`. -/
class MetricSpace (Œ± : Type u) extends PseudoMetricSpace Œ± : Type u where
  eq_of_dist_eq_zero : ‚àÄ {x y : Œ±}, dist x y = 0 ‚Üí x = y
#align metric_space MetricSpace

/-- Two metric space structures with the same distance coincide. -/
@[ext]
theorem MetricSpace.ext {Œ± : Type*} {m m' : MetricSpace Œ±} (h : m.toDist = m'.toDist) :
    m = m' := by
  cases m; cases m'; congr; ext1; assumption
#align metric_space.ext MetricSpace.ext

/-- Construct a metric space structure whose underlying topological space structure
(definitionally) agrees which a pre-existing topology which is compatible with a given distance
function. -/
def MetricSpace.ofDistTopology {Œ± : Type u} [TopologicalSpace Œ±] (dist : Œ± ‚Üí Œ± ‚Üí ‚Ñù)
    (dist_self : ‚àÄ x : Œ±, dist x x = 0) (dist_comm : ‚àÄ x y : Œ±, dist x y = dist y x)
    (dist_triangle : ‚àÄ x y z : Œ±, dist x z ‚â§ dist x y + dist y z)
    (H : ‚àÄ s : Set Œ±, IsOpen s ‚Üî ‚àÄ x ‚àà s, ‚àÉ Œµ > 0, ‚àÄ y, dist x y < Œµ ‚Üí y ‚àà s)
    (eq_of_dist_eq_zero : ‚àÄ x y : Œ±, dist x y = 0 ‚Üí x = y) : MetricSpace Œ± :=
  { PseudoMetricSpace.ofDistTopology dist dist_self dist_comm dist_triangle H with
    eq_of_dist_eq_zero := eq_of_dist_eq_zero _ _ }
#align metric_space.of_dist_topology MetricSpace.ofDistTopology

variable {Œ≥ : Type w} [MetricSpace Œ≥]

theorem eq_of_dist_eq_zero {x y : Œ≥} : dist x y = 0 ‚Üí x = y :=
  MetricSpace.eq_of_dist_eq_zero
#align eq_of_dist_eq_zero eq_of_dist_eq_zero

@[simp]
theorem dist_eq_zero {x y : Œ≥} : dist x y = 0 ‚Üî x = y :=
  Iff.intro eq_of_dist_eq_zero fun this => this ‚ñ∏ dist_self _
#align dist_eq_zero dist_eq_zero

@[simp]
theorem zero_eq_dist {x y : Œ≥} : 0 = dist x y ‚Üî x = y := by rw [eq_comm, dist_eq_zero]
#align zero_eq_dist zero_eq_dist

theorem dist_ne_zero {x y : Œ≥} : dist x y ‚â† 0 ‚Üî x ‚â† y := by
  simpa only [not_iff_not] using dist_eq_zero
#align dist_ne_zero dist_ne_zero

@[simp]
theorem dist_le_zero {x y : Œ≥} : dist x y ‚â§ 0 ‚Üî x = y := by
  simpa [le_antisymm_iff, dist_nonneg] using @dist_eq_zero _ _ x y
#align dist_le_zero dist_le_zero

@[simp]
theorem dist_pos {x y : Œ≥} : 0 < dist x y ‚Üî x ‚â† y := by
  simpa only [not_le] using not_congr dist_le_zero
#align dist_pos dist_pos

theorem eq_of_forall_dist_le {x y : Œ≥} (h : ‚àÄ Œµ > 0, dist x y ‚â§ Œµ) : x = y :=
  eq_of_dist_eq_zero (eq_of_le_of_forall_le_of_dense dist_nonneg h)
#align eq_of_forall_dist_le eq_of_forall_dist_le

/-- Deduce the equality of points with the vanishing of the nonnegative distance-/
theorem eq_of_nndist_eq_zero {x y : Œ≥} : nndist x y = 0 ‚Üí x = y := by
  simp only [‚Üê NNReal.eq_iff, ‚Üê dist_nndist, imp_self, NNReal.coe_zero, dist_eq_zero]
#align eq_of_nndist_eq_zero eq_of_nndist_eq_zero

/-- Characterize the equality of points with the vanishing of the nonnegative distance-/
@[simp]
theorem nndist_eq_zero {x y : Œ≥} : nndist x y = 0 ‚Üî x = y := by
  simp only [‚Üê NNReal.eq_iff, ‚Üê dist_nndist, imp_self, NNReal.coe_zero, dist_eq_zero]
#align nndist_eq_zero nndist_eq_zero

@[simp]
theorem zero_eq_nndist {x y : Œ≥} : 0 = nndist x y ‚Üî x = y := by
  simp only [‚Üê NNReal.eq_iff, ‚Üê dist_nndist, imp_self, NNReal.coe_zero, zero_eq_dist]
#align zero_eq_nndist zero_eq_nndist

namespace Metric

variable {x : Œ≥} {s : Set Œ≥}

@[simp] theorem closedBall_zero : closedBall x 0 = {x} := Set.ext fun _ => dist_le_zero
#align metric.closed_ball_zero Metric.closedBall_zero

@[simp] theorem sphere_zero : sphere x 0 = {x} := Set.ext fun _ => dist_eq_zero
#align metric.sphere_zero Metric.sphere_zero

theorem subsingleton_closedBall (x : Œ≥) {r : ‚Ñù} (hr : r ‚â§ 0) : (closedBall x r).Subsingleton := by
  rcases hr.lt_or_eq with (hr | rfl)
  ¬∑ rw [closedBall_eq_empty.2 hr]
    exact subsingleton_empty
  ¬∑ rw [closedBall_zero]
    exact subsingleton_singleton
#align metric.subsingleton_closed_ball Metric.subsingleton_closedBall

theorem subsingleton_sphere (x : Œ≥) {r : ‚Ñù} (hr : r ‚â§ 0) : (sphere x r).Subsingleton :=
  (subsingleton_closedBall x hr).anti sphere_subset_closedBall
#align metric.subsingleton_sphere Metric.subsingleton_sphere

-- see Note [lower instance priority]
instance (priority := 100) _root_.MetricSpace.to_separated : SeparatedSpace Œ≥ :=
  separated_def.2 fun _ _ h =>
    eq_of_forall_dist_le fun _ Œµ0 => le_of_lt (h _ (dist_mem_uniformity Œµ0))
#align metric_space.to_separated MetricSpace.to_separated

/-- A map between metric spaces is a uniform embedding if and only if the distance between `f x`
and `f y` is controlled in terms of the distance between `x` and `y` and conversely. -/
theorem uniformEmbedding_iff' [MetricSpace Œ≤] {f : Œ≥ ‚Üí Œ≤} :
    UniformEmbedding f ‚Üî
      (‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ {a b : Œ≥}, dist a b < Œ¥ ‚Üí dist (f a) (f b) < Œµ) ‚àß
        ‚àÄ Œ¥ > 0, ‚àÉ Œµ > 0, ‚àÄ {a b : Œ≥}, dist (f a) (f b) < Œµ ‚Üí dist a b < Œ¥ := by
  rw [uniformEmbedding_iff_uniformInducing, uniformInducing_iff, uniformContinuous_iff]
#align metric.uniform_embedding_iff' Metric.uniformEmbedding_iff'

/-- If a `PseudoMetricSpace` is a T‚ÇÄ space, then it is a `MetricSpace`. -/
@[reducible]
def _root_.MetricSpace.ofT0PseudoMetricSpace (Œ± : Type*) [PseudoMetricSpace Œ±] [T0Space Œ±] :
    MetricSpace Œ± where
  toPseudoMetricSpace := ‚Äπ_‚Ä∫
  eq_of_dist_eq_zero hdist := (Metric.inseparable_iff.2 hdist).eq
#align metric_space.of_t0_pseudo_metric_space MetricSpace.ofT0PseudoMetricSpace

-- see Note [lower instance priority]
/-- A metric space induces an emetric space -/
instance (priority := 100) _root_.MetricSpace.toEMetricSpace : EMetricSpace Œ≥ :=
  .ofT0PseudoEMetricSpace Œ≥
#align metric_space.to_emetric_space MetricSpace.toEMetricSpace

theorem isClosed_of_pairwise_le_dist {s : Set Œ≥} {Œµ : ‚Ñù} (hŒµ : 0 < Œµ)
    (hs : s.Pairwise fun x y => Œµ ‚â§ dist x y) : IsClosed s :=
  isClosed_of_spaced_out (dist_mem_uniformity hŒµ) <| by simpa using hs
#align metric.is_closed_of_pairwise_le_dist Metric.isClosed_of_pairwise_le_dist

theorem closedEmbedding_of_pairwise_le_dist {Œ± : Type*} [TopologicalSpace Œ±] [DiscreteTopology Œ±]
    {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) {f : Œ± ‚Üí Œ≥} (hf : Pairwise fun x y => Œµ ‚â§ dist (f x) (f y)) :
    ClosedEmbedding f :=
  closedEmbedding_of_spaced_out (dist_mem_uniformity hŒµ) <| by simpa using hf
#align metric.closed_embedding_of_pairwise_le_dist Metric.closedEmbedding_of_pairwise_le_dist

/-- If `f : Œ≤ ‚Üí Œ±` sends any two distinct points to points at distance at least `Œµ > 0`, then
`f` is a uniform embedding with respect to the discrete uniformity on `Œ≤`. -/
theorem uniformEmbedding_bot_of_pairwise_le_dist {Œ≤ : Type*} {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) {f : Œ≤ ‚Üí Œ±}
    (hf : Pairwise fun x y => Œµ ‚â§ dist (f x) (f y)) :
    @UniformEmbedding _ _ ‚ä• (by infer_instance) f :=
  uniformEmbedding_of_spaced_out (dist_mem_uniformity hŒµ) <| by simpa using hf
#align metric.uniform_embedding_bot_of_pairwise_le_dist Metric.uniformEmbedding_bot_of_pairwise_le_dist

theorem finite_isBounded_inter_isClosed [ProperSpace Œ±] {K s : Set Œ±} [DiscreteTopology s]
    (hK : IsBounded K) (hs : IsClosed s) : Set.Finite (K ‚à© s) := by
  refine Set.Finite.subset (IsCompact.finite ?_ ?_) (Set.inter_subset_inter_left s subset_closure)
  ¬∑ exact hK.isCompact_closure.inter_right hs
  ¬∑ exact DiscreteTopology.of_subset inferInstance (Set.inter_subset_right _ s)

end Metric

/-- Build a new metric space from an old one where the bundled uniform structure is provably
(but typically non-definitionaly) equal to some given uniform structure.
See Note [forgetful inheritance].
-/
def MetricSpace.replaceUniformity {Œ≥} [U : UniformSpace Œ≥] (m : MetricSpace Œ≥)
    (H : ùì§[U] = ùì§[PseudoEMetricSpace.toUniformSpace]) : MetricSpace Œ≥ where
  toPseudoMetricSpace := PseudoMetricSpace.replaceUniformity m.toPseudoMetricSpace H
  eq_of_dist_eq_zero := @eq_of_dist_eq_zero _ _
#align metric_space.replace_uniformity MetricSpace.replaceUniformity

theorem MetricSpace.replaceUniformity_eq {Œ≥} [U : UniformSpace Œ≥] (m : MetricSpace Œ≥)
    (H : ùì§[U] = ùì§[PseudoEMetricSpace.toUniformSpace]) : m.replaceUniformity H = m := by
  ext; rfl
#align metric_space.replace_uniformity_eq MetricSpace.replaceUniformity_eq

/-- Build a new metric space from an old one where the bundled topological structure is provably
(but typically non-definitionaly) equal to some given topological structure.
See Note [forgetful inheritance].
-/
@[reducible]
def MetricSpace.replaceTopology {Œ≥} [U : TopologicalSpace Œ≥] (m : MetricSpace Œ≥)
    (H : U = m.toPseudoMetricSpace.toUniformSpace.toTopologicalSpace) : MetricSpace Œ≥ :=
  @MetricSpace.replaceUniformity Œ≥ (m.toUniformSpace.replaceTopology H) m rfl
#align metric_space.replace_topology MetricSpace.replaceTopology

theorem MetricSpace.replaceTopology_eq {Œ≥} [U : TopologicalSpace Œ≥] (m : MetricSpace Œ≥)
    (H : U = m.toPseudoMetricSpace.toUniformSpace.toTopologicalSpace) :
    m.replaceTopology H = m := by
  ext; rfl
#align metric_space.replace_topology_eq MetricSpace.replaceTopology_eq

/-- One gets a metric space from an emetric space if the edistance
is everywhere finite, by pushing the edistance to reals. We set it up so that the edist and the
uniformity are defeq in the metric space and the emetric space. In this definition, the distance
is given separately, to be able to prescribe some expression which is not defeq to the push-forward
of the edistance to reals. -/
@[reducible]
def EMetricSpace.toMetricSpaceOfDist {Œ± : Type u} [EMetricSpace Œ±] (dist : Œ± ‚Üí Œ± ‚Üí ‚Ñù)
    (edist_ne_top : ‚àÄ x y : Œ±, edist x y ‚â† ‚ä§) (h : ‚àÄ x y, dist x y = ENNReal.toReal (edist x y)) :
    MetricSpace Œ± :=
  @MetricSpace.ofT0PseudoMetricSpace _
    (PseudoEMetricSpace.toPseudoMetricSpaceOfDist dist edist_ne_top h) _
#align emetric_space.to_metric_space_of_dist EMetricSpace.toMetricSpaceOfDist

/-- One gets a metric space from an emetric space if the edistance
is everywhere finite, by pushing the edistance to reals. We set it up so that the edist and the
uniformity are defeq in the metric space and the emetric space. -/
def EMetricSpace.toMetricSpace {Œ± : Type u} [EMetricSpace Œ±] (h : ‚àÄ x y : Œ±, edist x y ‚â† ‚ä§) :
    MetricSpace Œ± :=
  EMetricSpace.toMetricSpaceOfDist (fun x y => ENNReal.toReal (edist x y)) h fun _ _ => rfl
#align emetric_space.to_metric_space EMetricSpace.toMetricSpace

/-- Build a new metric space from an old one where the bundled bornology structure is provably
(but typically non-definitionaly) equal to some given bornology structure.
See Note [forgetful inheritance].
-/
def MetricSpace.replaceBornology {Œ±} [B : Bornology Œ±] (m : MetricSpace Œ±)
    (H : ‚àÄ s, @IsBounded _ B s ‚Üî @IsBounded _ PseudoMetricSpace.toBornology s) : MetricSpace Œ± :=
  { PseudoMetricSpace.replaceBornology _ H, m with toBornology := B }
#align metric_space.replace_bornology MetricSpace.replaceBornology

theorem MetricSpace.replaceBornology_eq {Œ±} [m : MetricSpace Œ±] [B : Bornology Œ±]
    (H : ‚àÄ s, @IsBounded _ B s ‚Üî @IsBounded _ PseudoMetricSpace.toBornology s) :
    MetricSpace.replaceBornology _ H = m := by
  ext
  rfl
#align metric_space.replace_bornology_eq MetricSpace.replaceBornology_eq

/-- Metric space structure pulled back by an injective function. Injectivity is necessary to
ensure that `dist x y = 0` only if `x = y`. -/
@[reducible]
def MetricSpace.induced {Œ≥ Œ≤} (f : Œ≥ ‚Üí Œ≤) (hf : Function.Injective f) (m : MetricSpace Œ≤) :
    MetricSpace Œ≥ :=
  { PseudoMetricSpace.induced f m.toPseudoMetricSpace with
    eq_of_dist_eq_zero := fun h => hf (dist_eq_zero.1 h) }
#align metric_space.induced MetricSpace.induced

/-- Pull back a metric space structure by a uniform embedding. This is a version of
`MetricSpace.induced` useful in case if the domain already has a `UniformSpace` structure. -/
@[reducible]
def UniformEmbedding.comapMetricSpace {Œ± Œ≤} [UniformSpace Œ±] [m : MetricSpace Œ≤] (f : Œ± ‚Üí Œ≤)
    (h : UniformEmbedding f) : MetricSpace Œ± :=
  .replaceUniformity (.induced f h.inj m) h.comap_uniformity.symm
#align uniform_embedding.comap_metric_space UniformEmbedding.comapMetricSpace

/-- Pull back a metric space structure by an embedding. This is a version of
`MetricSpace.induced` useful in case if the domain already has a `TopologicalSpace` structure. -/
@[reducible]
def Embedding.comapMetricSpace {Œ± Œ≤} [TopologicalSpace Œ±] [m : MetricSpace Œ≤] (f : Œ± ‚Üí Œ≤)
    (h : Embedding f) : MetricSpace Œ± :=
  .replaceTopology (.induced f h.inj m) h.induced
#align embedding.comap_metric_space Embedding.comapMetricSpace

instance Subtype.metricSpace {Œ± : Type*} {p : Œ± ‚Üí Prop} [MetricSpace Œ±] :
    MetricSpace (Subtype p) :=
  .induced Subtype.val Subtype.coe_injective ‚Äπ_‚Ä∫
#align subtype.metric_space Subtype.metricSpace

@[to_additive]
instance {Œ± : Type*} [MetricSpace Œ±] : MetricSpace Œ±·µê·µí·µñ :=
  MetricSpace.induced MulOpposite.unop MulOpposite.unop_injective ‚Äπ_‚Ä∫

instance : MetricSpace Empty where
  dist _ _ := 0
  dist_self _ := rfl
  dist_comm _ _ := rfl
  edist _ _ := 0
  edist_dist _ _ := ENNReal.ofReal_zero.symm -- porting note: should not be needed
  eq_of_dist_eq_zero _ := Subsingleton.elim _ _
  dist_triangle _ _ _ := show (0 : ‚Ñù) ‚â§ 0 + 0 by rw [add_zero]
  toUniformSpace := inferInstance
  uniformity_dist := Subsingleton.elim _ _

instance : MetricSpace PUnit.{u + 1} where
  dist _ _ := 0
  dist_self _ := rfl
  dist_comm _ _ := rfl
  edist _ _ := 0
  edist_dist _ _ := ENNReal.ofReal_zero.symm -- porting note: should not be needed
  eq_of_dist_eq_zero _ := Subsingleton.elim _ _
  dist_triangle _ _ _ := show (0 : ‚Ñù) ‚â§ 0 + 0 by rw [add_zero]
  toUniformSpace := inferInstance
  uniformity_dist := by
    simp (config := { contextual := true }) [principal_univ, eq_top_of_neBot (ùì§ PUnit)]

section Real

/-- Instantiate the reals as a metric space. -/
instance Real.metricSpace : MetricSpace ‚Ñù := .ofT0PseudoMetricSpace ‚Ñù
#align real.metric_space Real.metricSpace

end Real

section NNReal

instance : MetricSpace ‚Ñù‚â•0 :=
  Subtype.metricSpace

end NNReal

instance [MetricSpace Œ≤] : MetricSpace (ULift Œ≤) :=
  MetricSpace.induced ULift.down ULift.down_injective ‚Äπ_‚Ä∫

section Prod

instance Prod.metricSpaceMax [MetricSpace Œ≤] : MetricSpace (Œ≥ √ó Œ≤) := .ofT0PseudoMetricSpace _
#align prod.metric_space_max Prod.metricSpaceMax

end Prod

section Pi

open Finset

variable {œÄ : Œ≤ ‚Üí Type*} [Fintype Œ≤] [‚àÄ b, MetricSpace (œÄ b)]

/-- A finite product of metric spaces is a metric space, with the sup distance. -/
instance metricSpacePi : MetricSpace (‚àÄ b, œÄ b) := .ofT0PseudoMetricSpace _
#align metric_space_pi metricSpacePi

end Pi

namespace Metric

section SecondCountable

open TopologicalSpace

-- porting note: todo: use `Countable` instead of `Encodable`
/-- A metric space is second countable if one can reconstruct up to any `Œµ>0` any element of the
space from countably many data. -/
theorem secondCountable_of_countable_discretization {Œ± : Type u} [MetricSpace Œ±]
    (H : ‚àÄ Œµ > (0 : ‚Ñù), ‚àÉ (Œ≤ : Type*) (_ : Encodable Œ≤) (F : Œ± ‚Üí Œ≤),
      ‚àÄ x y, F x = F y ‚Üí dist x y ‚â§ Œµ) :
    SecondCountableTopology Œ± := by
  refine secondCountable_of_almost_dense_set fun Œµ Œµ0 => ?_
  rcases H Œµ Œµ0 with ‚ü®Œ≤, fŒ≤, F, hF‚ü©
  let Finv := rangeSplitting F
  refine ‚ü®range Finv, ‚ü®countable_range _, fun x => ?_‚ü©‚ü©
  let x' := Finv ‚ü®F x, mem_range_self _‚ü©
  have : F x' = F x := apply_rangeSplitting F _
  exact ‚ü®x', mem_range_self _, hF _ _ this.symm‚ü©
#align metric.second_countable_of_countable_discretization Metric.secondCountable_of_countable_discretization

end SecondCountable

end Metric

section EqRel

instance {Œ± : Type u} [PseudoMetricSpace Œ±] : Dist (UniformSpace.SeparationQuotient Œ±) where
  dist p q := Quotient.liftOn‚ÇÇ' p q dist <| fun x y x' y' hx hy => by
    rw [dist_edist, dist_edist, ‚Üê UniformSpace.SeparationQuotient.edist_mk x,
      ‚Üê UniformSpace.SeparationQuotient.edist_mk x', Quot.sound hx, Quot.sound hy]

theorem UniformSpace.SeparationQuotient.dist_mk {Œ± : Type u} [PseudoMetricSpace Œ±] (p q : Œ±) :
    @dist (UniformSpace.SeparationQuotient Œ±) _ (Quot.mk _ p) (Quot.mk _ q) = dist p q :=
  rfl
#align uniform_space.separation_quotient.dist_mk UniformSpace.SeparationQuotient.dist_mk

instance {Œ± : Type u} [PseudoMetricSpace Œ±] : MetricSpace (UniformSpace.SeparationQuotient Œ±) :=
  EMetricSpace.toMetricSpaceOfDist dist (fun x y => Quotient.inductionOn‚ÇÇ' x y edist_ne_top)
    fun x y => Quotient.inductionOn‚ÇÇ' x y dist_edist

end EqRel

/-!
### `Additive`, `Multiplicative`

The distance on those type synonyms is inherited without change.
-/


open Additive Multiplicative

section

variable [Dist X]

instance : Dist (Additive X) := ‚ÄπDist X‚Ä∫
instance : Dist (Multiplicative X) := ‚ÄπDist X‚Ä∫

@[simp] theorem dist_ofMul (a b : X) : dist (ofMul a) (ofMul b) = dist a b := rfl
#align dist_of_mul dist_ofMul

@[simp] theorem dist_ofAdd (a b : X) : dist (ofAdd a) (ofAdd b) = dist a b := rfl
#align dist_of_add dist_ofAdd

@[simp] theorem dist_toMul (a b : Additive X) : dist (toMul a) (toMul b) = dist a b := rfl
#align dist_to_mul dist_toMul

@[simp] theorem dist_toAdd (a b : Multiplicative X) : dist (toAdd a) (toAdd b) = dist a b := rfl
#align dist_to_add dist_toAdd

end

section

variable [PseudoMetricSpace X]

instance : PseudoMetricSpace (Additive X) := ‚ÄπPseudoMetricSpace X‚Ä∫
instance : PseudoMetricSpace (Multiplicative X) := ‚ÄπPseudoMetricSpace X‚Ä∫

@[simp] theorem nndist_ofMul (a b : X) : nndist (ofMul a) (ofMul b) = nndist a b := rfl
#align nndist_of_mul nndist_ofMul

@[simp] theorem nndist_ofAdd (a b : X) : nndist (ofAdd a) (ofAdd b) = nndist a b := rfl
#align nndist_of_add nndist_ofAdd

@[simp] theorem nndist_toMul (a b : Additive X) : nndist (toMul a) (toMul b) = nndist a b := rfl
#align nndist_to_mul nndist_toMul

@[simp]
theorem nndist_toAdd (a b : Multiplicative X) : nndist (toAdd a) (toAdd b) = nndist a b := rfl
#align nndist_to_add nndist_toAdd

end

instance [MetricSpace X] : MetricSpace (Additive X) := ‚ÄπMetricSpace X‚Ä∫
instance [MetricSpace X] : MetricSpace (Multiplicative X) := ‚ÄπMetricSpace X‚Ä∫

instance [PseudoMetricSpace X] [ProperSpace X] : ProperSpace (Additive X) := ‚ÄπProperSpace X‚Ä∫
instance [PseudoMetricSpace X] [ProperSpace X] : ProperSpace (Multiplicative X) := ‚ÄπProperSpace X‚Ä∫

/-!
### Order dual

The distance on this type synonym is inherited without change.
-/

open OrderDual

section

variable [Dist X]

instance : Dist X·µí·µà := ‚ÄπDist X‚Ä∫

@[simp] theorem dist_toDual (a b : X) : dist (toDual a) (toDual b) = dist a b := rfl
#align dist_to_dual dist_toDual

@[simp] theorem dist_ofDual (a b : X·µí·µà) : dist (ofDual a) (ofDual b) = dist a b := rfl
#align dist_of_dual dist_ofDual

end

section

variable [PseudoMetricSpace X]

instance : PseudoMetricSpace X·µí·µà := ‚ÄπPseudoMetricSpace X‚Ä∫

@[simp] theorem nndist_toDual (a b : X) : nndist (toDual a) (toDual b) = nndist a b := rfl
#align nndist_to_dual nndist_toDual

@[simp] theorem nndist_ofDual (a b : X·µí·µà) : nndist (ofDual a) (ofDual b) = nndist a b := rfl
#align nndist_of_dual nndist_ofDual

end

instance [MetricSpace X] : MetricSpace X·µí·µà := ‚ÄπMetricSpace X‚Ä∫

instance [PseudoMetricSpace X] [ProperSpace X] : ProperSpace X·µí·µà := ‚ÄπProperSpace X‚Ä∫
