/-
Copyright (c) 2023 Felix Weilacher. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Felix Weilacher
-/
import Mathlib.Topology.MetricSpace.PiNat

#align_import topology.metric_space.cantor_scheme from "leanprover-community/mathlib"@"49b7f94aab3a3bdca1f9f34c5d818afb253b3993"

/-!
# (Topological) Schemes and their induced maps

In topology, and especially descriptive set theory, one often constructs functions `(â„• â†’ Î²) â†’ Î±`,
where Î± is some topological space and Î² is a discrete space, as an appropriate limit of some map
`List Î² â†’ Set Î±`. We call the latter type of map a "`Î²`-scheme on `Î±`".

This file develops the basic, abstract theory of these schemes and the functions they induce.

## Main Definitions

* `CantorScheme.inducedMap A` : The aforementioned "limit" of a scheme `A : List Î² â†’ Set Î±`.
  This is a partial function from `â„• â†’ Î²` to `a`,
  implemented here as an object of type `Î£ s : Set (â„• â†’ Î²), s â†’ Î±`.
  That is, `(inducedMap A).1` is the domain and `(inducedMap A).2` is the function.

## Implementation Notes

We consider end-appending to be the fundamental way to build lists (say on `Î²`) inductively,
as this interacts better with the topology on `â„• â†’ Î²`.
As a result, functions like `List.get?` or `Stream'.take` do not have their intended meaning
in this file. See instead `PiNat.res`.

## References

* [kechris1995] (Chapters 6-7)

## Tags

scheme, cantor scheme, lusin scheme, approximation.

-/


namespace CantorScheme

open List Function Filter Set PiNat

open Classical Topology

variable {Î² Î± : Type*} (A : List Î² â†’ Set Î±)

/-- From a `Î²`-scheme on `Î±` `A`, we define a partial function from `(â„• â†’ Î²)` to `Î±`
which sends each infinite sequence `x` to an element of the intersection along the
branch corresponding to `x`, if it exists.
We call this the map induced by the scheme. -/
noncomputable def inducedMap : Î£s : Set (â„• â†’ Î²), s â†’ Î± :=
  âŸ¨fun x => Set.Nonempty (â‹‚ n : â„•, A (res x n)), fun x => x.property.someâŸ©
#align cantor_scheme.induced_map CantorScheme.inducedMap

section Topology

/-- A scheme is antitone if each set contains its children. -/
protected def Antitone : Prop :=
  âˆ€ l : List Î², âˆ€ a : Î², A (a :: l) âŠ† A l
#align cantor_scheme.antitone CantorScheme.Antitone

/-- A useful strengthening of being antitone is to require that each set contains
the closure of each of its children. -/
def ClosureAntitone [TopologicalSpace Î±] : Prop :=
  âˆ€ l : List Î², âˆ€ a : Î², closure (A (a :: l)) âŠ† A l
#align cantor_scheme.closure_antitone CantorScheme.ClosureAntitone

/-- A scheme is disjoint if the children of each set of pairwise disjoint. -/
protected def Disjoint : Prop :=
  âˆ€ l : List Î², Pairwise fun a b => Disjoint (A (a :: l)) (A (b :: l))
#align cantor_scheme.disjoint CantorScheme.Disjoint

variable {A}

/-- If `x` is in the domain of the induced map of a scheme `A`,
its image under this map is in each set along the corresponding branch. -/
theorem map_mem (x : (inducedMap A).1) (n : â„•) : (inducedMap A).2 x âˆˆ A (res x n) := by
  have := x.property.some_mem
  -- âŠ¢ Sigma.snd (inducedMap A) x âˆˆ A (res (â†‘x) n)
  rw [mem_iInter] at this
  -- âŠ¢ Sigma.snd (inducedMap A) x âˆˆ A (res (â†‘x) n)
  exact this n
  -- ğŸ‰ no goals
#align cantor_scheme.map_mem CantorScheme.map_mem

protected theorem ClosureAntitone.antitone [TopologicalSpace Î±] (hA : ClosureAntitone A) :
    CantorScheme.Antitone A := fun l a => subset_closure.trans (hA l a)
#align cantor_scheme.closure_antitone.antitone CantorScheme.ClosureAntitone.antitone

protected theorem Antitone.closureAntitone [TopologicalSpace Î±] (hanti : CantorScheme.Antitone A)
    (hclosed : âˆ€ l, IsClosed (A l)) : ClosureAntitone A := fun _ _ =>
  (hclosed _).closure_eq.subset.trans (hanti _ _)
#align cantor_scheme.antitone.closure_antitone CantorScheme.Antitone.closureAntitone

/-- A scheme where the children of each set are pairwise disjoint induces an injective map. -/
theorem Disjoint.map_injective (hA : CantorScheme.Disjoint A) : Injective (inducedMap A).2 := by
  rintro âŸ¨x, hxâŸ© âŸ¨y, hyâŸ© hxy
  -- âŠ¢ { val := x, property := hx } = { val := y, property := hy }
  refine' Subtype.coe_injective (res_injective _)
  -- âŠ¢ res ((fun a => â†‘a) { val := x, property := hx }) = res ((fun a => â†‘a) { val  â€¦
  dsimp
  -- âŠ¢ res x = res y
  ext n : 1
  -- âŠ¢ res x n = res y n
  induction' n with n ih; Â· simp
  -- âŠ¢ res x Nat.zero = res y Nat.zero
                            -- ğŸ‰ no goals
  simp only [res_succ, cons.injEq]
  -- âŠ¢ x n = y n âˆ§ res x n = res y n
  refine' âŸ¨_, ihâŸ©
  -- âŠ¢ x n = y n
  contrapose hA
  -- âŠ¢ Â¬CantorScheme.Disjoint A
  simp only [CantorScheme.Disjoint, _root_.Pairwise, Ne.def, not_forall, exists_prop]
  -- âŠ¢ âˆƒ x x_1 x_2, Â¬x_1 = x_2 âˆ§ Â¬_root_.Disjoint (A (x_1 :: x)) (A (x_2 :: x))
  refine' âŸ¨res x n, _, _, hA, _âŸ©
  -- âŠ¢ Â¬_root_.Disjoint (A (x n :: res x n)) (A (y n :: res x n))
  rw [not_disjoint_iff]
  -- âŠ¢ âˆƒ x_1, x_1 âˆˆ A (x n :: res x n) âˆ§ x_1 âˆˆ A (y n :: res x n)
  refine' âŸ¨(inducedMap A).2 âŸ¨x, hxâŸ©, _, _âŸ©
  -- âŠ¢ Sigma.snd (inducedMap A) { val := x, property := hx } âˆˆ A (x n :: res x n)
  Â· rw [â† res_succ]
    -- âŠ¢ Sigma.snd (inducedMap A) { val := x, property := hx } âˆˆ A (res x (Nat.succ n))
    apply map_mem
    -- ğŸ‰ no goals
  rw [hxy, ih, â† res_succ]
  -- âŠ¢ Sigma.snd (inducedMap A) { val := y, property := hy } âˆˆ A (res y (Nat.succ n))
  apply map_mem
  -- ğŸ‰ no goals
#align cantor_scheme.disjoint.map_injective CantorScheme.Disjoint.map_injective

end Topology

section Metric

variable [PseudoMetricSpace Î±]

/-- A scheme on a metric space has vanishing diameter if diameter approaches 0 along each branch. -/
def VanishingDiam : Prop :=
  âˆ€ x : â„• â†’ Î², Tendsto (fun n : â„• => EMetric.diam (A (res x n))) atTop (ğ“ 0)
#align cantor_scheme.vanishing_diam CantorScheme.VanishingDiam

variable {A}

theorem VanishingDiam.dist_lt (hA : VanishingDiam A) (Îµ : â„) (Îµ_pos : 0 < Îµ) (x : â„• â†’ Î²) :
    âˆƒ n : â„•, âˆ€ (y) (_ : y âˆˆ A (res x n)) (z) (_ : z âˆˆ A (res x n)), dist y z < Îµ := by
  specialize hA x
  -- âŠ¢ âˆƒ n, âˆ€ (y : Î±), y âˆˆ A (res x n) â†’ âˆ€ (z : Î±), z âˆˆ A (res x n) â†’ dist y z < Îµ
  rw [ENNReal.tendsto_atTop_zero] at hA
  -- âŠ¢ âˆƒ n, âˆ€ (y : Î±), y âˆˆ A (res x n) â†’ âˆ€ (z : Î±), z âˆˆ A (res x n) â†’ dist y z < Îµ
  cases' hA (ENNReal.ofReal (Îµ / 2)) (by
    simp only [gt_iff_lt, ENNReal.ofReal_pos]
    linarith) with n hn
  use n
  -- âŠ¢ âˆ€ (y : Î±), y âˆˆ A (res x n) â†’ âˆ€ (z : Î±), z âˆˆ A (res x n) â†’ dist y z < Îµ
  intro y hy z hz
  -- âŠ¢ dist y z < Îµ
  rw [â† ENNReal.ofReal_lt_ofReal_iff Îµ_pos, â† edist_dist]
  -- âŠ¢ edist y z < ENNReal.ofReal Îµ
  apply lt_of_le_of_lt (EMetric.edist_le_diam_of_mem hy hz)
  -- âŠ¢ EMetric.diam (A (res x n)) < ENNReal.ofReal Îµ
  apply lt_of_le_of_lt (hn _ (le_refl _))
  -- âŠ¢ ENNReal.ofReal (Îµ / 2) < ENNReal.ofReal Îµ
  rw [ENNReal.ofReal_lt_ofReal_iff Îµ_pos]
  -- âŠ¢ Îµ / 2 < Îµ
  linarith
  -- ğŸ‰ no goals
#align cantor_scheme.vanishing_diam.dist_lt CantorScheme.VanishingDiam.dist_lt

/-- A scheme with vanishing diameter along each branch induces a continuous map. -/
theorem VanishingDiam.map_continuous [TopologicalSpace Î²] [DiscreteTopology Î²]
    (hA : VanishingDiam A) : Continuous (inducedMap A).2 := by
  rw [Metric.continuous_iff']
  -- âŠ¢ âˆ€ (a : â†‘(inducedMap A).fst) (Îµ : â„), Îµ > 0 â†’ âˆ€á¶  (x : â†‘(inducedMap A).fst) in â€¦
  rintro âŸ¨x, hxâŸ© Îµ Îµ_pos
  -- âŠ¢ âˆ€á¶  (x_1 : â†‘(inducedMap A).fst) in ğ“ { val := x, property := hx }, dist (Sigm â€¦
  cases' hA.dist_lt _ Îµ_pos x with n hn
  -- âŠ¢ âˆ€á¶  (x_1 : â†‘(inducedMap A).fst) in ğ“ { val := x, property := hx }, dist (Sigm â€¦
  rw [_root_.eventually_nhds_iff]
  -- âŠ¢ âˆƒ t, (âˆ€ (x_1 : â†‘(inducedMap A).fst), x_1 âˆˆ t â†’ dist (Sigma.snd (inducedMap A â€¦
  refine' âŸ¨(â†‘)â»Â¹' cylinder x n, _, _, by simpâŸ©
  -- âŠ¢ âˆ€ (x_1 : â†‘(inducedMap A).fst), x_1 âˆˆ Subtype.val â»Â¹' cylinder x n â†’ dist (Si â€¦
  Â· rintro âŸ¨y, hyâŸ© hyx
    -- âŠ¢ dist (Sigma.snd (inducedMap A) { val := y, property := hy }) (Sigma.snd (ind â€¦
    rw [mem_preimage, Subtype.coe_mk, cylinder_eq_res, mem_setOf] at hyx
    -- âŠ¢ dist (Sigma.snd (inducedMap A) { val := y, property := hy }) (Sigma.snd (ind â€¦
    apply hn
    -- âŠ¢ Sigma.snd (inducedMap A) { val := y, property := hy } âˆˆ A (res x n)
    Â· rw [â† hyx]
      -- âŠ¢ Sigma.snd (inducedMap A) { val := y, property := hy } âˆˆ A (res y n)
      apply map_mem
      -- ğŸ‰ no goals
    apply map_mem
    -- ğŸ‰ no goals
  apply continuous_subtype_val.isOpen_preimage
  -- âŠ¢ IsOpen (cylinder x n)
  apply isOpen_cylinder
  -- ğŸ‰ no goals
#align cantor_scheme.vanishing_diam.map_continuous CantorScheme.VanishingDiam.map_continuous

/-- A scheme on a complete space with vanishing diameter
such that each set contains the closure of its children
induces a total map. -/
theorem ClosureAntitone.map_of_vanishingDiam [CompleteSpace Î±] (hdiam : VanishingDiam A)
    (hanti : ClosureAntitone A) (hnonempty : âˆ€ l, (A l).Nonempty) : (inducedMap A).1 = univ := by
  rw [eq_univ_iff_forall]
  -- âŠ¢ âˆ€ (x : â„• â†’ Î²), x âˆˆ (inducedMap A).fst
  intro x
  -- âŠ¢ x âˆˆ (inducedMap A).fst
  choose u hu using fun n => hnonempty (res x n)
  -- âŠ¢ x âˆˆ (inducedMap A).fst
  have umem : âˆ€ n m : â„•, n â‰¤ m â†’ u m âˆˆ A (res x n) := by
    have : Antitone fun n : â„• => A (res x n) := by
      refine' antitone_nat_of_succ_le _
      intro n
      apply hanti.antitone
    intro n m hnm
    exact this hnm (hu _)
  have : CauchySeq u := by
    rw [Metric.cauchySeq_iff]
    intro Îµ Îµ_pos
    cases' hdiam.dist_lt _ Îµ_pos x with n hn
    use n
    intro mâ‚€ hmâ‚€ mâ‚ hmâ‚
    apply hn <;> apply umem <;> assumption
  cases' cauchySeq_tendsto_of_complete this with y hy
  -- âŠ¢ x âˆˆ (inducedMap A).fst
  use y
  -- âŠ¢ y âˆˆ â‹‚ (n : â„•), A (res x n)
  rw [mem_iInter]
  -- âŠ¢ âˆ€ (i : â„•), y âˆˆ A (res x i)
  intro n
  -- âŠ¢ y âˆˆ A (res x n)
  apply hanti _ (x n)
  -- âŠ¢ y âˆˆ closure (A (x n :: res x n))
  apply mem_closure_of_tendsto hy
  -- âŠ¢ âˆ€á¶  (x_1 : â„•) in atTop, u x_1 âˆˆ A (x n :: res x n)
  rw [eventually_atTop]
  -- âŠ¢ âˆƒ a, âˆ€ (b : â„•), b â‰¥ a â†’ u b âˆˆ A (x n :: res x n)
  exact âŸ¨n.succ, umem _âŸ©
  -- ğŸ‰ no goals
#align cantor_scheme.closure_antitone.map_of_vanishing_diam CantorScheme.ClosureAntitone.map_of_vanishingDiam

end Metric

end CantorScheme
