/-
Copyright (c) 2021 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
module

public import Mathlib.Topology.Algebra.MulAction
public import Mathlib.Topology.Algebra.SeparationQuotient.Basic
public import Mathlib.Topology.Algebra.UniformMulAction
public import Mathlib.Topology.MetricSpace.Lipschitz
import Mathlib.Topology.Order.LiminfLimsup

/-!
# Compatibility of algebraic operations with metric space structures

In this file we define mixin typeclasses `LipschitzMul`, `LipschitzAdd`,
`IsBoundedSMul` expressing compatibility of multiplication, addition and scalar-multiplication
operations with an underlying metric space structure.  The intended use case is to abstract certain
properties shared by normed groups and by `Râ‰¥0`.

## Implementation notes

We deduce a `ContinuousMul` instance from `LipschitzMul`, etc.  In principle there should
be an intermediate typeclass for uniform spaces, but the algebraic hierarchy there (see
`IsUniformGroup`) is structured differently.

-/

@[expose] public section

open NNReal Filter Set
open scoped Topology Uniformity

noncomputable section

variable (Î± Î² : Type*) [PseudoMetricSpace Î±] [PseudoMetricSpace Î²]

section LipschitzMul

/-- Class `LipschitzAdd M` says that the addition `(+) : X Ã— X â†’ X` is Lipschitz jointly in
the two arguments. -/
class LipschitzAdd [AddMonoid Î²] : Prop where
  lipschitz_add : âˆƒ C, LipschitzWith C fun p : Î² Ã— Î² => p.1 + p.2

/-- Class `LipschitzMul M` says that the multiplication `(*) : X Ã— X â†’ X` is Lipschitz jointly
in the two arguments. -/
@[to_additive]
class LipschitzMul [Monoid Î²] : Prop where
  lipschitz_mul : âˆƒ C, LipschitzWith C fun p : Î² Ã— Î² => p.1 * p.2

variable [Monoid Î²]

/-- The Lipschitz constant of a monoid `Î²` satisfying `LipschitzMul` -/
@[to_additive /-- The Lipschitz constant of an `AddMonoid` `Î²` satisfying `LipschitzAdd` -/]
def LipschitzMul.C [_i : LipschitzMul Î²] : â„â‰¥0 := Classical.choose _i.lipschitz_mul

variable {Î²}

@[to_additive]
theorem lipschitzWith_lipschitz_const_mul_edist [_i : LipschitzMul Î²] :
    LipschitzWith (LipschitzMul.C Î²) fun p : Î² Ã— Î² => p.1 * p.2 :=
  Classical.choose_spec _i.lipschitz_mul

variable [LipschitzMul Î²]

@[to_additive]
theorem lipschitz_with_lipschitz_const_mul :
    âˆ€ p q : Î² Ã— Î², dist (p.1 * p.2) (q.1 * q.2) â‰¤ LipschitzMul.C Î² * dist p q := by
  rw [â† lipschitzWith_iff_dist_le_mul]
  exact lipschitzWith_lipschitz_const_mul_edist

-- see Note [lower instance priority]
@[to_additive]
instance (priority := 100) LipschitzMul.continuousMul : ContinuousMul Î² :=
  âŸ¨lipschitzWith_lipschitz_const_mul_edist.continuousâŸ©

@[to_additive]
instance Submonoid.lipschitzMul (s : Submonoid Î²) : LipschitzMul s where
  lipschitz_mul := âŸ¨LipschitzMul.C Î², by
    rintro âŸ¨xâ‚, xâ‚‚âŸ© âŸ¨yâ‚, yâ‚‚âŸ©
    convert lipschitzWith_lipschitz_const_mul_edist âŸ¨(xâ‚ : Î²), xâ‚‚âŸ© âŸ¨yâ‚, yâ‚‚âŸ© using 1âŸ©

@[to_additive]
instance MulOpposite.lipschitzMul : LipschitzMul Î²áµáµ’áµ– where
  lipschitz_mul := âŸ¨LipschitzMul.C Î², fun âŸ¨xâ‚, xâ‚‚âŸ© âŸ¨yâ‚, yâ‚‚âŸ© =>
    (lipschitzWith_lipschitz_const_mul_edist âŸ¨xâ‚‚.unop, xâ‚.unopâŸ© âŸ¨yâ‚‚.unop, yâ‚.unopâŸ©).trans_eq
      (congr_arg _ <| max_comm _ _)âŸ©

-- this instance could be deduced from `NormedAddCommGroup.lipschitzAdd`, but we prove it
-- separately here so that it is available earlier in the hierarchy
instance Real.hasLipschitzAdd : LipschitzAdd â„ where
  lipschitz_add := âŸ¨2, LipschitzWith.of_dist_le_mul fun p q => by
    simp only [Real.dist_eq, Prod.dist_eq, NNReal.coe_ofNat,
      add_sub_add_comm, two_mul]
    refine le_trans (abs_add_le (p.1 - q.1) (p.2 - q.2)) ?_
    exact add_le_add (le_max_left _ _) (le_max_right _ _)âŸ©

-- this instance has the same proof as `AddSubmonoid.lipschitzAdd`, but the former can't
-- directly be applied here since `â„â‰¥0` is a subtype of `â„`, not an additive submonoid.
instance NNReal.hasLipschitzAdd : LipschitzAdd â„â‰¥0 where
  lipschitz_add := âŸ¨LipschitzAdd.C â„, by
    rintro âŸ¨xâ‚, xâ‚‚âŸ© âŸ¨yâ‚, yâ‚‚âŸ©
    exact lipschitzWith_lipschitz_const_add_edist âŸ¨(xâ‚ : â„), xâ‚‚âŸ© âŸ¨yâ‚, yâ‚‚âŸ©âŸ©

end LipschitzMul

section IsBoundedSMul

variable [Zero Î±] [Zero Î²] [SMul Î± Î²]

/-- Mixin typeclass on a scalar action of a metric space `Î±` on a metric space `Î²` both with
distinguished points `0`, requiring compatibility of the action in the sense that
`dist (x â€¢ yâ‚) (x â€¢ yâ‚‚) â‰¤ dist x 0 * dist yâ‚ yâ‚‚` and
`dist (xâ‚ â€¢ y) (xâ‚‚ â€¢ y) â‰¤ dist xâ‚ xâ‚‚ * dist y 0`.

If `[NormedDivisionRing Î±] [SeminormedAddCommGroup Î²] [Module Î± Î²]` are assumed, then prefer writing
`[NormSMulClass Î± Î²]` instead of using `[IsBoundedSMul Î± Î²]`, since while equivalent, typeclass
search can only infer the latter from the former and not vice versa. -/
class IsBoundedSMul : Prop where
  dist_smul_pair' : âˆ€ x : Î±, âˆ€ yâ‚ yâ‚‚ : Î², dist (x â€¢ yâ‚) (x â€¢ yâ‚‚) â‰¤ dist x 0 * dist yâ‚ yâ‚‚
  dist_pair_smul' : âˆ€ xâ‚ xâ‚‚ : Î±, âˆ€ y : Î², dist (xâ‚ â€¢ y) (xâ‚‚ â€¢ y) â‰¤ dist xâ‚ xâ‚‚ * dist y 0

variable {Î± Î²}
variable [IsBoundedSMul Î± Î²]

theorem dist_smul_pair (x : Î±) (yâ‚ yâ‚‚ : Î²) : dist (x â€¢ yâ‚) (x â€¢ yâ‚‚) â‰¤ dist x 0 * dist yâ‚ yâ‚‚ :=
  IsBoundedSMul.dist_smul_pair' x yâ‚ yâ‚‚

theorem dist_pair_smul (xâ‚ xâ‚‚ : Î±) (y : Î²) : dist (xâ‚ â€¢ y) (xâ‚‚ â€¢ y) â‰¤ dist xâ‚ xâ‚‚ * dist y 0 :=
  IsBoundedSMul.dist_pair_smul' xâ‚ xâ‚‚ y

theorem Bornology.IsBounded.uniformContinuousOn_smul {s : Set (Î± Ã— Î²)} (hs : IsBounded s) :
    UniformContinuousOn (Â· â€¢ Â·).uncurry s := by
  rcases hs.subset_ball_lt 0 0 with âŸ¨C, hCâ‚€, hCâŸ©
  rw [Metric.uniformContinuousOn_iff_le]
  intro Îµ hÎµ
  refine âŸ¨Îµ / (2 * C), by positivity, fun âŸ¨a, bâŸ© hab âŸ¨x, yâŸ© hxy h â†¦ ?_âŸ©
  grw [hC, Metric.mem_ball, Prod.dist_eq, max_lt_iff] at hab hxy
  rw [Prod.dist_eq, max_le_iff] at h
  dsimp at hab hxy h âŠ¢
  grw [dist_triangle _ (a â€¢ y), dist_pair_smul, dist_smul_pair, hab.1, hxy.2, h.2, h.1]
  field_simp
  norm_num1

-- see Note [lower instance priority]
/-- The typeclass `IsBoundedSMul` on a metric-space scalar action implies continuity of the
action. -/
instance (priority := 100) IsBoundedSMul.continuousSMul : ContinuousSMul Î± Î² where
  continuous_smul := by
    rw [continuous_iff_continuousAt]
    intro x
    refine Metric.isBounded_ball (x := 0) (r := dist x 0 + 1) |>.uniformContinuousOn_smul
      |>.continuousOn |>.continuousAt ?_
    exact Metric.isOpen_ball.mem_nhds (by simp)

instance (priority := 100) IsBoundedSMul.toUniformContinuousConstSMul :
    UniformContinuousConstSMul Î± Î² :=
  âŸ¨fun c => ((lipschitzWith_iff_dist_le_mul (K := nndist c 0)).2 fun _ _ =>
    dist_smul_pair c _ _).uniformContinuousâŸ©

@[to_fun]
theorem TendstoLocallyUniformlyOn.smulâ‚€_of_isBoundedUnder {X Î¹ : Type*} [TopologicalSpace X]
    {s : Set X} {F : Î¹ â†’ X â†’ Î±} {G : Î¹ â†’ X â†’ Î²} {f : X â†’ Î±} {g : X â†’ Î²} {l : Filter Î¹}
    (hF : TendstoLocallyUniformlyOn F f l s) (hG : TendstoLocallyUniformlyOn G g l s)
    (hf : âˆ€ x âˆˆ s, (ğ“[s] x).IsBoundedUnder (Â· â‰¤ Â·) (fun y â†¦ dist (f y) 0))
    (hg : âˆ€ x âˆˆ s, (ğ“[s] x).IsBoundedUnder (Â· â‰¤ Â·) (fun y â†¦ dist (g y) 0)) :
    TendstoLocallyUniformlyOn (F â€¢ G) (f â€¢ g) l s := by
  have H := hF.prodMk hG
  rw [tendstoLocallyUniformlyOn_iff_forall_tendsto] at *
  intro x hx
  rcases (hf x hx).sup (hg x hx) with âŸ¨C, hCâŸ©
  simp_rw [Filter.eventually_map, max_le_iff] at hC
  refine Tendsto.comp
    (Metric.isBounded_ball (x := (0 : Î± Ã— Î²)) (r := C + 1)).uniformContinuousOn_smul
    (tendsto_inf.mpr âŸ¨H x hx, tendsto_principal.mpr ?_âŸ©)
  filter_upwards [hF x hx (Metric.dist_mem_uniformity one_pos),
    hG x hx (Metric.dist_mem_uniformity one_pos), tendsto_snd hC] with âŸ¨n, yâŸ© hFn hGn hfg
  simp only [mem_prod, Metric.mem_ball, Prod.dist_eq, Prod.fst_zero, Prod.snd_zero, sup_lt_iff,
    mem_preimage, mem_setOf] at hFn hGn hfg âŠ¢
  grw [dist_triangle_left (F n y) 0 (f y), dist_triangle_left (G n y) 0 (g y)]
  constructor <;> constructor <;> linarith

@[to_fun]
theorem TendstoLocallyUniformlyOn.mulâ‚€_of_isBoundedUnder {X M Î¹ : Type*} [TopologicalSpace X]
    [PseudoMetricSpace M] [Zero M] [Mul M] [IsBoundedSMul M M]
    {s : Set X} {F : Î¹ â†’ X â†’ M} {G : Î¹ â†’ X â†’ M} {f : X â†’ M} {g : X â†’ M} {l : Filter Î¹}
    (hF : TendstoLocallyUniformlyOn F f l s) (hG : TendstoLocallyUniformlyOn G g l s)
    (hf : âˆ€ x âˆˆ s, (ğ“[s] x).IsBoundedUnder (Â· â‰¤ Â·) (fun y â†¦ dist (f y) 0))
    (hg : âˆ€ x âˆˆ s, (ğ“[s] x).IsBoundedUnder (Â· â‰¤ Â·) (fun y â†¦ dist (g y) 0)) :
    TendstoLocallyUniformlyOn (F * G) (f * g) l s :=
  hF.smulâ‚€_of_isBoundedUnder hG hf hg

@[to_fun]
theorem TendstoLocallyUniformly.smulâ‚€_of_isBoundedUnder {X Î¹ : Type*} [TopologicalSpace X]
    {F : Î¹ â†’ X â†’ Î±} {G : Î¹ â†’ X â†’ Î²} {f : X â†’ Î±} {g : X â†’ Î²} {l : Filter Î¹}
    (hF : TendstoLocallyUniformly F f l) (hG : TendstoLocallyUniformly G g l)
    (hf : âˆ€ x, (ğ“ x).IsBoundedUnder (Â· â‰¤ Â·) (fun y â†¦ dist (f y) 0))
    (hg : âˆ€ x, (ğ“ x).IsBoundedUnder (Â· â‰¤ Â·) (fun y â†¦ dist (g y) 0)) :
    TendstoLocallyUniformly (F â€¢ G) (f â€¢ g) l := by
  rw [â† tendstoLocallyUniformlyOn_univ] at *
  apply hF.smulâ‚€_of_isBoundedUnder hG <;> simpa

@[to_fun]
theorem TendstoLocallyUniformly.mulâ‚€_of_isBoundedUnder {X M Î¹ : Type*} [TopologicalSpace X]
    [PseudoMetricSpace M] [Zero M] [Mul M] [IsBoundedSMul M M]
    {F : Î¹ â†’ X â†’ M} {G : Î¹ â†’ X â†’ M} {f : X â†’ M} {g : X â†’ M} {l : Filter Î¹}
    (hF : TendstoLocallyUniformly F f l) (hG : TendstoLocallyUniformly G g l)
    (hf : âˆ€ x, (ğ“ x).IsBoundedUnder (Â· â‰¤ Â·) (fun y â†¦ dist (f y) 0))
    (hg : âˆ€ x, (ğ“ x).IsBoundedUnder (Â· â‰¤ Â·) (fun y â†¦ dist (g y) 0)) :
    TendstoLocallyUniformly (F * G) (f * g) l :=
  hF.smulâ‚€_of_isBoundedUnder hG hf hg

@[to_fun]
theorem TendstoLocallyUniformlyOn.smulâ‚€ {X Î¹ : Type*} [TopologicalSpace X]
    {s : Set X} {F : Î¹ â†’ X â†’ Î±} {G : Î¹ â†’ X â†’ Î²} {f : X â†’ Î±} {g : X â†’ Î²} {l : Filter Î¹}
    (hF : TendstoLocallyUniformlyOn F f l s) (hG : TendstoLocallyUniformlyOn G g l s)
    (hfc : ContinuousOn f s) (hgc : ContinuousOn g s) :
    TendstoLocallyUniformlyOn (F â€¢ G) (f â€¢ g) l s :=
  hF.smulâ‚€_of_isBoundedUnder hG
    (fun x hx â†¦ ((hfc x hx).dist tendsto_const_nhds).isBoundedUnder_le)
    (fun x hx â†¦ ((hgc x hx).dist tendsto_const_nhds).isBoundedUnder_le)

@[to_fun]
theorem TendstoLocallyUniformlyOn.mulâ‚€ {X M Î¹ : Type*} [TopologicalSpace X]
    [PseudoMetricSpace M] [Zero M] [Mul M] [IsBoundedSMul M M]
    {s : Set X} {F : Î¹ â†’ X â†’ M} {G : Î¹ â†’ X â†’ M} {f : X â†’ M} {g : X â†’ M} {l : Filter Î¹}
    (hF : TendstoLocallyUniformlyOn F f l s) (hG : TendstoLocallyUniformlyOn G g l s)
    (hf : ContinuousOn f s) (hg : ContinuousOn g s) :
    TendstoLocallyUniformlyOn (F * G) (f * g) l s :=
  hF.smulâ‚€ hG hf hg

@[to_fun]
theorem TendstoLocallyUniformly.smulâ‚€ {X Î¹ : Type*} [TopologicalSpace X]
    {F : Î¹ â†’ X â†’ Î±} {G : Î¹ â†’ X â†’ Î²} {f : X â†’ Î±} {g : X â†’ Î²} {l : Filter Î¹}
    (hF : TendstoLocallyUniformly F f l) (hG : TendstoLocallyUniformly G g l)
    (hfc : Continuous f) (hgc : Continuous g) :
    TendstoLocallyUniformly (F â€¢ G) (f â€¢ g) l :=
  hF.smulâ‚€_of_isBoundedUnder hG
    (fun x â†¦ ((hfc.tendsto x).dist tendsto_const_nhds).isBoundedUnder_le)
    (fun x â†¦ ((hgc.tendsto x).dist tendsto_const_nhds).isBoundedUnder_le)

@[to_fun]
theorem TendstoLocallyUniformly.mulâ‚€ {X M Î¹ : Type*} [TopologicalSpace X]
    [PseudoMetricSpace M] [Zero M] [Mul M] [IsBoundedSMul M M]
    {F : Î¹ â†’ X â†’ M} {G : Î¹ â†’ X â†’ M} {f : X â†’ M} {g : X â†’ M} {l : Filter Î¹}
    (hF : TendstoLocallyUniformly F f l) (hG : TendstoLocallyUniformly G g l)
    (hf : Continuous f) (hg : Continuous g) :
    TendstoLocallyUniformly (F * G) (f * g) l :=
  hF.smulâ‚€ hG hf hg

-- this instance could be deduced from `NormedSpace.isBoundedSMul`, but we prove it separately
-- here so that it is available earlier in the hierarchy
instance Real.isBoundedSMul : IsBoundedSMul â„ â„ where
  dist_smul_pair' x yâ‚ yâ‚‚ := by simpa [Real.dist_eq, mul_sub] using (abs_mul x (yâ‚ - yâ‚‚)).le
  dist_pair_smul' xâ‚ xâ‚‚ y := by simpa [Real.dist_eq, sub_mul] using (abs_mul (xâ‚ - xâ‚‚) y).le

instance NNReal.isBoundedSMul : IsBoundedSMul â„â‰¥0 â„â‰¥0 where
  dist_smul_pair' x yâ‚ yâ‚‚ := by convert dist_smul_pair (x : â„) (yâ‚ : â„) yâ‚‚ using 1
  dist_pair_smul' xâ‚ xâ‚‚ y := by convert dist_pair_smul (xâ‚ : â„) xâ‚‚ (y : â„) using 1

/-- If a scalar is central, then its right action is bounded when its left action is. -/
instance IsBoundedSMul.op [SMul Î±áµáµ’áµ– Î²] [IsCentralScalar Î± Î²] : IsBoundedSMul Î±áµáµ’áµ– Î² where
  dist_smul_pair' :=
    MulOpposite.rec' fun x yâ‚ yâ‚‚ => by simpa only [op_smul_eq_smul] using dist_smul_pair x yâ‚ yâ‚‚
  dist_pair_smul' :=
    MulOpposite.rec' fun xâ‚ =>
      MulOpposite.rec' fun xâ‚‚ y => by simpa only [op_smul_eq_smul] using dist_pair_smul xâ‚ xâ‚‚ y

end IsBoundedSMul

instance [Monoid Î±] [LipschitzMul Î±] : LipschitzAdd (Additive Î±) :=
  âŸ¨@LipschitzMul.lipschitz_mul Î± _ _ _âŸ©

instance [AddMonoid Î±] [LipschitzAdd Î±] : LipschitzMul (Multiplicative Î±) :=
  âŸ¨@LipschitzAdd.lipschitz_add Î± _ _ _âŸ©

@[to_additive]
instance [Monoid Î±] [LipschitzMul Î±] : LipschitzMul Î±áµ’áµˆ :=
  â€¹LipschitzMul Î±â€º

variable {Î¹ : Type*} [Fintype Î¹]

instance Pi.instIsBoundedSMul {Î± : Type*} {Î² : Î¹ â†’ Type*} [PseudoMetricSpace Î±]
    [âˆ€ i, PseudoMetricSpace (Î² i)] [Zero Î±] [âˆ€ i, Zero (Î² i)] [âˆ€ i, SMul Î± (Î² i)]
    [âˆ€ i, IsBoundedSMul Î± (Î² i)] : IsBoundedSMul Î± (âˆ€ i, Î² i) where
  dist_smul_pair' x yâ‚ yâ‚‚ :=
    (dist_pi_le_iff <| by positivity).2 fun _ â†¦
      (dist_smul_pair _ _ _).trans <| mul_le_mul_of_nonneg_left (dist_le_pi_dist _ _ _) dist_nonneg
  dist_pair_smul' xâ‚ xâ‚‚ y :=
    (dist_pi_le_iff <| by positivity).2 fun _ â†¦
      (dist_pair_smul _ _ _).trans <| mul_le_mul_of_nonneg_left (dist_le_pi_dist _ 0 _) dist_nonneg

instance Pi.instIsBoundedSMul' {Î± Î² : Î¹ â†’ Type*} [âˆ€ i, PseudoMetricSpace (Î± i)]
    [âˆ€ i, PseudoMetricSpace (Î² i)] [âˆ€ i, Zero (Î± i)] [âˆ€ i, Zero (Î² i)] [âˆ€ i, SMul (Î± i) (Î² i)]
    [âˆ€ i, IsBoundedSMul (Î± i) (Î² i)] : IsBoundedSMul (âˆ€ i, Î± i) (âˆ€ i, Î² i) where
  dist_smul_pair' x yâ‚ yâ‚‚ :=
    (dist_pi_le_iff <| by positivity).2 fun _ â†¦
      (dist_smul_pair _ _ _).trans <|
        mul_le_mul (dist_le_pi_dist _ 0 _) (dist_le_pi_dist _ _ _) dist_nonneg dist_nonneg
  dist_pair_smul' xâ‚ xâ‚‚ y :=
    (dist_pi_le_iff <| by positivity).2 fun _ â†¦
      (dist_pair_smul _ _ _).trans <|
        mul_le_mul (dist_le_pi_dist _ _ _) (dist_le_pi_dist _ 0 _) dist_nonneg dist_nonneg

instance Prod.instIsBoundedSMul {Î± Î² Î³ : Type*} [PseudoMetricSpace Î±] [PseudoMetricSpace Î²]
    [PseudoMetricSpace Î³] [Zero Î±] [Zero Î²] [Zero Î³] [SMul Î± Î²] [SMul Î± Î³] [IsBoundedSMul Î± Î²]
    [IsBoundedSMul Î± Î³] : IsBoundedSMul Î± (Î² Ã— Î³) where
  dist_smul_pair' _x _yâ‚ _yâ‚‚ :=
    max_le ((dist_smul_pair _ _ _).trans <| mul_le_mul_of_nonneg_left (le_max_left _ _) dist_nonneg)
      ((dist_smul_pair _ _ _).trans <| mul_le_mul_of_nonneg_left (le_max_right _ _) dist_nonneg)
  dist_pair_smul' _xâ‚ _xâ‚‚ _y :=
    max_le ((dist_pair_smul _ _ _).trans <| mul_le_mul_of_nonneg_left (le_max_left _ _) dist_nonneg)
      ((dist_pair_smul _ _ _).trans <| mul_le_mul_of_nonneg_left (le_max_right _ _) dist_nonneg)

instance {Î± Î² : Type*}
    [PseudoMetricSpace Î±] [PseudoMetricSpace Î²] [Zero Î±] [Zero Î²] [SMul Î± Î²] [IsBoundedSMul Î± Î²] :
    IsBoundedSMul Î± (SeparationQuotient Î²) where
  dist_smul_pair' _ := Quotient.indâ‚‚ <| dist_smul_pair _
  dist_pair_smul' _ _ := Quotient.ind <| dist_pair_smul _ _

-- We don't have the `SMul Î± Î³ â†’ SMul Î² Î´ â†’ SMul (Î± Ã— Î²) (Î³ Ã— Î´)` instance, but if we did, then
-- `IsBoundedSMul Î± Î³ â†’ IsBoundedSMul Î² Î´ â†’ IsBoundedSMul (Î± Ã— Î²) (Î³ Ã— Î´)` would hold
