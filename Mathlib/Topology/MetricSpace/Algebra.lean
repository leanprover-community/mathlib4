/-
Copyright (c) 2021 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import Mathlib.Tactic.Monotonicity
import Mathlib.Topology.Algebra.MulAction
import Mathlib.Topology.MetricSpace.Lipschitz

#align_import topology.metric_space.algebra from "leanprover-community/mathlib"@"14d34b71b6d896b6e5f1ba2ec9124b9cd1f90fca"

/-!
# Compatibility of algebraic operations with metric space structures

In this file we define mixin typeclasses `LipschitzMul`, `LipschitzAdd`,
`BoundedSMul` expressing compatibility of multiplication, addition and scalar-multiplication
operations with an underlying metric space structure.  The intended use case is to abstract certain
properties shared by normed groups and by `R‚â•0`.

## Implementation notes

We deduce a `ContinuousMul` instance from `LipschitzMul`, etc.  In principle there should
be an intermediate typeclass for uniform spaces, but the algebraic hierarchy there (see
`UniformGroup`) is structured differently.

-/


open NNReal

noncomputable section

variable (Œ± Œ≤ : Type*) [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤]

section LipschitzMul

/-- Class `LipschitzAdd M` says that the addition `(+) : X √ó X ‚Üí X` is Lipschitz jointly in
the two arguments. -/
class LipschitzAdd [AddMonoid Œ≤] : Prop where
  lipschitz_add : ‚àÉ C, LipschitzWith C fun p : Œ≤ √ó Œ≤ => p.1 + p.2
#align has_lipschitz_add LipschitzAdd

/-- Class `LipschitzMul M` says that the multiplication `(*) : X √ó X ‚Üí X` is Lipschitz jointly
in the two arguments. -/
@[to_additive]
class LipschitzMul [Monoid Œ≤] : Prop where
  lipschitz_mul : ‚àÉ C, LipschitzWith C fun p : Œ≤ √ó Œ≤ => p.1 * p.2
#align has_lipschitz_mul LipschitzMul

/-- The Lipschitz constant of an `AddMonoid` `Œ≤` satisfying `LipschitzAdd` -/
def LipschitzAdd.C [AddMonoid Œ≤] [_i : LipschitzAdd Œ≤] : ‚Ñù‚â•0 := Classical.choose _i.lipschitz_add
set_option linter.uppercaseLean3 false in
#align has_lipschitz_add.C LipschitzAdd.C

variable [Monoid Œ≤]

/-- The Lipschitz constant of a monoid `Œ≤` satisfying `LipschitzMul` -/
@[to_additive existing] -- porting note: had to add `LipschitzAdd.C`. to_additive silently failed
def LipschitzMul.C [_i : LipschitzMul Œ≤] : ‚Ñù‚â•0 := Classical.choose _i.lipschitz_mul
set_option linter.uppercaseLean3 false in
#align has_lipschitz_mul.C LipschitzMul.C

variable {Œ≤}

@[to_additive]
theorem lipschitzWith_lipschitz_const_mul_edist [_i : LipschitzMul Œ≤] :
    LipschitzWith (LipschitzMul.C Œ≤) fun p : Œ≤ √ó Œ≤ => p.1 * p.2 :=
  Classical.choose_spec _i.lipschitz_mul
#align lipschitz_with_lipschitz_const_mul_edist lipschitzWith_lipschitz_const_mul_edist
#align lipschitz_with_lipschitz_const_add_edist lipschitzWith_lipschitz_const_add_edist

variable [LipschitzMul Œ≤]

@[to_additive]
theorem lipschitz_with_lipschitz_const_mul :
    ‚àÄ p q : Œ≤ √ó Œ≤, dist (p.1 * p.2) (q.1 * q.2) ‚â§ LipschitzMul.C Œ≤ * dist p q := by
  rw [‚Üê lipschitzWith_iff_dist_le_mul]
  -- ‚ä¢ LipschitzWith (LipschitzMul.C Œ≤) fun p => p.fst * p.snd
  exact lipschitzWith_lipschitz_const_mul_edist
  -- üéâ no goals
#align lipschitz_with_lipschitz_const_mul lipschitz_with_lipschitz_const_mul
#align lipschitz_with_lipschitz_const_add lipschitz_with_lipschitz_const_add

-- see Note [lower instance priority]
@[to_additive]
instance (priority := 100) LipschitzMul.continuousMul : ContinuousMul Œ≤ :=
  ‚ü®lipschitzWith_lipschitz_const_mul_edist.continuous‚ü©
#align has_lipschitz_mul.has_continuous_mul LipschitzMul.continuousMul
#align has_lipschitz_add.has_continuous_add LipschitzAdd.continuousAdd

@[to_additive]
instance Submonoid.lipschitzMul (s : Submonoid Œ≤) : LipschitzMul s where
  lipschitz_mul := ‚ü®LipschitzMul.C Œ≤, by
    rintro ‚ü®x‚ÇÅ, x‚ÇÇ‚ü© ‚ü®y‚ÇÅ, y‚ÇÇ‚ü©
    -- ‚ä¢ edist ((fun p => p.fst * p.snd) (x‚ÇÅ, x‚ÇÇ)) ((fun p => p.fst * p.snd) (y‚ÇÅ, y‚ÇÇ) ‚Ä¶
    convert lipschitzWith_lipschitz_const_mul_edist ‚ü®(x‚ÇÅ : Œ≤), x‚ÇÇ‚ü© ‚ü®y‚ÇÅ, y‚ÇÇ‚ü© using 1‚ü©
    -- üéâ no goals
#align submonoid.has_lipschitz_mul Submonoid.lipschitzMul
#align add_submonoid.has_lipschitz_add AddSubmonoid.lipschitzAdd

@[to_additive]
instance MulOpposite.lipschitzMul : LipschitzMul Œ≤·µê·µí·µñ where
  lipschitz_mul := ‚ü®LipschitzMul.C Œ≤, fun ‚ü®x‚ÇÅ, x‚ÇÇ‚ü© ‚ü®y‚ÇÅ, y‚ÇÇ‚ü© =>
    (lipschitzWith_lipschitz_const_mul_edist ‚ü®x‚ÇÇ.unop, x‚ÇÅ.unop‚ü© ‚ü®y‚ÇÇ.unop, y‚ÇÅ.unop‚ü©).trans_eq
      (congr_arg _ <| max_comm _ _)‚ü©
#align mul_opposite.has_lipschitz_mul MulOpposite.lipschitzMul
#align add_opposite.has_lipschitz_add AddOpposite.lipschitzAdd

-- this instance could be deduced from `NormedAddCommGroup.lipschitzAdd`, but we prove it
-- separately here so that it is available earlier in the hierarchy
instance Real.hasLipschitzAdd : LipschitzAdd ‚Ñù where
  lipschitz_add := ‚ü®2, LipschitzWith.of_dist_le_mul <| fun p q => by
    simp only [Real.dist_eq, Prod.dist_eq, Prod.fst_sub, Prod.snd_sub, NNReal.coe_ofNat,
      add_sub_add_comm, two_mul]
    refine le_trans (abs_add (p.1 - q.1) (p.2 - q.2)) ?_
    -- ‚ä¢ |p.fst - q.fst| + |p.snd - q.snd| ‚â§ max |p.fst - q.fst| |p.snd - q.snd| + ma ‚Ä¶
    exact add_le_add (le_max_left _ _) (le_max_right _ _)‚ü©
    -- üéâ no goals
#align real.has_lipschitz_add Real.hasLipschitzAdd

-- this instance has the same proof as `AddSubmonoid.lipschitzAdd`, but the former can't
-- directly be applied here since `‚Ñù‚â•0` is a subtype of `‚Ñù`, not an additive submonoid.
instance NNReal.hasLipschitzAdd : LipschitzAdd ‚Ñù‚â•0 where
  lipschitz_add := ‚ü®LipschitzAdd.C ‚Ñù, by
    rintro ‚ü®x‚ÇÅ, x‚ÇÇ‚ü© ‚ü®y‚ÇÅ, y‚ÇÇ‚ü©
    -- ‚ä¢ edist ((fun p => p.fst + p.snd) (x‚ÇÅ, x‚ÇÇ)) ((fun p => p.fst + p.snd) (y‚ÇÅ, y‚ÇÇ) ‚Ä¶
    exact lipschitzWith_lipschitz_const_add_edist ‚ü®(x‚ÇÅ : ‚Ñù), x‚ÇÇ‚ü© ‚ü®y‚ÇÅ, y‚ÇÇ‚ü©‚ü©
    -- üéâ no goals
#align nnreal.has_lipschitz_add NNReal.hasLipschitzAdd

end LipschitzMul

section BoundedSMul

variable [Zero Œ±] [Zero Œ≤] [SMul Œ± Œ≤]

/-- Mixin typeclass on a scalar action of a metric space `Œ±` on a metric space `Œ≤` both with
distinguished points `0`, requiring compatibility of the action in the sense that
`dist (x ‚Ä¢ y‚ÇÅ) (x ‚Ä¢ y‚ÇÇ) ‚â§ dist x 0 * dist y‚ÇÅ y‚ÇÇ` and
`dist (x‚ÇÅ ‚Ä¢ y) (x‚ÇÇ ‚Ä¢ y) ‚â§ dist x‚ÇÅ x‚ÇÇ * dist y 0`. -/
class BoundedSMul : Prop where
  dist_smul_pair' : ‚àÄ x : Œ±, ‚àÄ y‚ÇÅ y‚ÇÇ : Œ≤, dist (x ‚Ä¢ y‚ÇÅ) (x ‚Ä¢ y‚ÇÇ) ‚â§ dist x 0 * dist y‚ÇÅ y‚ÇÇ
  dist_pair_smul' : ‚àÄ x‚ÇÅ x‚ÇÇ : Œ±, ‚àÄ y : Œ≤, dist (x‚ÇÅ ‚Ä¢ y) (x‚ÇÇ ‚Ä¢ y) ‚â§ dist x‚ÇÅ x‚ÇÇ * dist y 0
#align has_bounded_smul BoundedSMul

variable {Œ± Œ≤}
variable [BoundedSMul Œ± Œ≤]

theorem dist_smul_pair (x : Œ±) (y‚ÇÅ y‚ÇÇ : Œ≤) : dist (x ‚Ä¢ y‚ÇÅ) (x ‚Ä¢ y‚ÇÇ) ‚â§ dist x 0 * dist y‚ÇÅ y‚ÇÇ :=
  BoundedSMul.dist_smul_pair' x y‚ÇÅ y‚ÇÇ
#align dist_smul_pair dist_smul_pair

theorem dist_pair_smul (x‚ÇÅ x‚ÇÇ : Œ±) (y : Œ≤) : dist (x‚ÇÅ ‚Ä¢ y) (x‚ÇÇ ‚Ä¢ y) ‚â§ dist x‚ÇÅ x‚ÇÇ * dist y 0 :=
  BoundedSMul.dist_pair_smul' x‚ÇÅ x‚ÇÇ y
#align dist_pair_smul dist_pair_smul

-- see Note [lower instance priority]
/-- The typeclass `BoundedSMul` on a metric-space scalar action implies continuity of the action. -/
instance (priority := 100) BoundedSMul.continuousSMul : ContinuousSMul Œ± Œ≤ where
  continuous_smul := by
    rw [Metric.continuous_iff]
    -- ‚ä¢ ‚àÄ (b : Œ± √ó Œ≤) (Œµ : ‚Ñù), Œµ > 0 ‚Üí ‚àÉ Œ¥, Œ¥ > 0 ‚àß ‚àÄ (a : Œ± √ó Œ≤), dist a b < Œ¥ ‚Üí di ‚Ä¶
    rintro ‚ü®a, b‚ü© Œµ Œµ0
    -- ‚ä¢ ‚àÉ Œ¥, Œ¥ > 0 ‚àß ‚àÄ (a_1 : Œ± √ó Œ≤), dist a_1 (a, b) < Œ¥ ‚Üí dist (a_1.fst ‚Ä¢ a_1.snd) ‚Ä¶
    obtain ‚ü®Œ¥, Œ¥0, hŒ¥Œµ‚ü© : ‚àÉ Œ¥ > 0, Œ¥ * (Œ¥ + dist b 0) + dist a 0 * Œ¥ < Œµ
    -- ‚ä¢ ‚àÉ Œ¥, Œ¥ > 0 ‚àß Œ¥ * (Œ¥ + dist b 0) + dist a 0 * Œ¥ < Œµ
    ¬∑ have : Continuous fun Œ¥ ‚Ü¶ Œ¥ * (Œ¥ + dist b 0) + dist a 0 * Œ¥ := by continuity
      -- ‚ä¢ ‚àÉ Œ¥, Œ¥ > 0 ‚àß Œ¥ * (Œ¥ + dist b 0) + dist a 0 * Œ¥ < Œµ
      refine ((this.tendsto' _ _ ?_).eventually (gt_mem_nhds Œµ0)).exists_gt
      -- ‚ä¢ 0 * (0 + dist b 0) + dist a 0 * 0 = 0
      simp
      -- üéâ no goals
    refine ‚ü®Œ¥, Œ¥0, fun (a', b') hab' => ?_‚ü©
    -- ‚ä¢ dist ((a', b').fst ‚Ä¢ (a', b').snd) ((a, b).fst ‚Ä¢ (a, b).snd) < Œµ
    obtain ‚ü®ha, hb‚ü© := max_lt_iff.1 hab'
    -- ‚ä¢ dist ((a', b').fst ‚Ä¢ (a', b').snd) ((a, b).fst ‚Ä¢ (a, b).snd) < Œµ
    calc dist (a' ‚Ä¢ b') (a ‚Ä¢ b)
        ‚â§ dist (a' ‚Ä¢ b') (a ‚Ä¢ b') + dist (a ‚Ä¢ b') (a ‚Ä¢ b) := dist_triangle ..
      _ ‚â§ dist a' a * dist b' 0 + dist a 0 * dist b' b :=
        add_le_add (dist_pair_smul _ _ _) (dist_smul_pair _ _ _)
      _ ‚â§ Œ¥ * (Œ¥ + dist b 0) + dist a 0 * Œ¥ := by
          have : dist b' 0 ‚â§ Œ¥ + dist b 0 := (dist_triangle _ _ _).trans <| add_le_add_right hb.le _
          mono* <;> apply_rules [dist_nonneg, le_of_lt]
      _ < Œµ := hŒ¥Œµ
#align has_bounded_smul.has_continuous_smul BoundedSMul.continuousSMul

-- this instance could be deduced from `NormedSpace.boundedSMul`, but we prove it separately
-- here so that it is available earlier in the hierarchy
instance Real.boundedSMul : BoundedSMul ‚Ñù ‚Ñù where
  dist_smul_pair' x y‚ÇÅ y‚ÇÇ := by simpa [Real.dist_eq, mul_sub] using (abs_mul x (y‚ÇÅ - y‚ÇÇ)).le
                                -- üéâ no goals
  dist_pair_smul' x‚ÇÅ x‚ÇÇ y := by simpa [Real.dist_eq, sub_mul] using (abs_mul (x‚ÇÅ - x‚ÇÇ) y).le
                                -- üéâ no goals
#align real.has_bounded_smul Real.boundedSMul

instance NNReal.boundedSMul : BoundedSMul ‚Ñù‚â•0 ‚Ñù‚â•0 where
  dist_smul_pair' x y‚ÇÅ y‚ÇÇ := by convert dist_smul_pair (x : ‚Ñù) (y‚ÇÅ : ‚Ñù) y‚ÇÇ using 1
                                -- üéâ no goals
  dist_pair_smul' x‚ÇÅ x‚ÇÇ y := by convert dist_pair_smul (x‚ÇÅ : ‚Ñù) x‚ÇÇ (y : ‚Ñù) using 1
                                -- üéâ no goals
#align nnreal.has_bounded_smul NNReal.boundedSMul

/-- If a scalar is central, then its right action is bounded when its left action is. -/
instance BoundedSMul.op [SMul Œ±·µê·µí·µñ Œ≤] [IsCentralScalar Œ± Œ≤] : BoundedSMul Œ±·µê·µí·µñ Œ≤ where
  dist_smul_pair' :=
    MulOpposite.rec' fun x y‚ÇÅ y‚ÇÇ => by simpa only [op_smul_eq_smul] using dist_smul_pair x y‚ÇÅ y‚ÇÇ
                                       -- üéâ no goals
  dist_pair_smul' :=
    MulOpposite.rec' fun x‚ÇÅ =>
      MulOpposite.rec' fun x‚ÇÇ y => by simpa only [op_smul_eq_smul] using dist_pair_smul x‚ÇÅ x‚ÇÇ y
                                      -- üéâ no goals
#align has_bounded_smul.op BoundedSMul.op

end BoundedSMul

instance [Monoid Œ±] [LipschitzMul Œ±] : LipschitzAdd (Additive Œ±) :=
  ‚ü®@LipschitzMul.lipschitz_mul Œ± _ _ _‚ü©

instance [AddMonoid Œ±] [LipschitzAdd Œ±] : LipschitzMul (Multiplicative Œ±) :=
  ‚ü®@LipschitzAdd.lipschitz_add Œ± _ _ _‚ü©

@[to_additive]
instance [Monoid Œ±] [LipschitzMul Œ±] : LipschitzMul Œ±·µí·µà :=
  ‚ÄπLipschitzMul Œ±‚Ä∫
