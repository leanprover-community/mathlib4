/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.ContDiff.RCLike
import Mathlib.MeasureTheory.Measure.Hausdorff

#align_import topology.metric_space.hausdorff_dimension from "leanprover-community/mathlib"@"8f9fea08977f7e450770933ee6abb20733b47c92"

/-!
# Hausdorff dimension

The Hausdorff dimension of a set `X` in an (extended) metric space is the unique number
`dimH s : ‚Ñù‚â•0‚àû` such that for any `d : ‚Ñù‚â•0` we have

- `ŒºH[d] s = 0` if `dimH s < d`, and
- `ŒºH[d] s = ‚àû` if `d < dimH s`.

In this file we define `dimH s` to be the Hausdorff dimension of `s`, then prove some basic
properties of Hausdorff dimension.

## Main definitions

* `MeasureTheory.dimH`: the Hausdorff dimension of a set. For the Hausdorff dimension of the whole
  space we use `MeasureTheory.dimH (Set.univ : Set X)`.

## Main results

### Basic properties of Hausdorff dimension

* `hausdorffMeasure_of_lt_dimH`, `dimH_le_of_hausdorffMeasure_ne_top`,
  `le_dimH_of_hausdorffMeasure_eq_top`, `hausdorffMeasure_of_dimH_lt`, `measure_zero_of_dimH_lt`,
  `le_dimH_of_hausdorffMeasure_ne_zero`, `dimH_of_hausdorffMeasure_ne_zero_ne_top`: various forms
  of the characteristic property of the Hausdorff dimension;
* `dimH_union`: the Hausdorff dimension of the union of two sets is the maximum of their Hausdorff
  dimensions.
* `dimH_iUnion`, `dimH_bUnion`, `dimH_sUnion`: the Hausdorff dimension of a countable union of sets
  is the supremum of their Hausdorff dimensions;
* `dimH_empty`, `dimH_singleton`, `Set.Subsingleton.dimH_zero`, `Set.Countable.dimH_zero` : `dimH s
  = 0` whenever `s` is countable;

### (Pre)images under (anti)lipschitz and H√∂lder continuous maps

* `HolderWith.dimH_image_le` etc: if `f : X ‚Üí Y` is H√∂lder continuous with exponent `r > 0`, then
  for any `s`, `dimH (f '' s) ‚â§ dimH s / r`. We prove versions of this statement for `HolderWith`,
  `HolderOnWith`, and locally H√∂lder maps, as well as for `Set.image` and `Set.range`.
* `LipschitzWith.dimH_image_le` etc: Lipschitz continuous maps do not increase the Hausdorff
  dimension of sets.
* for a map that is known to be both Lipschitz and antilipschitz (e.g., for an `Isometry` or
  a `ContinuousLinearEquiv`) we also prove `dimH (f '' s) = dimH s`.

### Hausdorff measure in `‚Ñù‚Åø`

* `Real.dimH_of_nonempty_interior`: if `s` is a set in a finite dimensional real vector space `E`
  with nonempty interior, then the Hausdorff dimension of `s` is equal to the dimension of `E`.
* `dense_compl_of_dimH_lt_finrank`: if `s` is a set in a finite dimensional real vector space `E`
  with Hausdorff dimension strictly less than the dimension of `E`, the `s` has a dense complement.
* `ContDiff.dense_compl_range_of_finrank_lt_finrank`: the complement to the range of a `C¬π`
  smooth map is dense provided that the dimension of the domain is strictly less than the dimension
  of the codomain.

## Notations

We use the following notation localized in `MeasureTheory`. It is defined in
`MeasureTheory.Measure.Hausdorff`.

- `ŒºH[d]` : `MeasureTheory.Measure.hausdorffMeasure d`

## Implementation notes

* The definition of `dimH` explicitly uses `borel X` as a measurable space structure. This way we
  can formulate lemmas about Hausdorff dimension without assuming that the environment has a
  `[MeasurableSpace X]` instance that is equal but possibly not defeq to `borel X`.

  Lemma `dimH_def` unfolds this definition using whatever `[MeasurableSpace X]` instance we have in
  the environment (as long as it is equal to `borel X`).

* The definition `dimH` is irreducible; use API lemmas or `dimH_def` instead.

## Tags

Hausdorff measure, Hausdorff dimension, dimension
-/


open scoped MeasureTheory ENNReal NNReal Topology

open MeasureTheory MeasureTheory.Measure Set TopologicalSpace FiniteDimensional Filter

variable {Œπ X Y : Type*} [EMetricSpace X] [EMetricSpace Y]

/-- Hausdorff dimension of a set in an (e)metric space. -/
@[irreducible] noncomputable def dimH (s : Set X) : ‚Ñù‚â•0‚àû := by
  borelize X; exact ‚®Ü (d : ‚Ñù‚â•0) (_ : @hausdorffMeasure X _ _ ‚ü®rfl‚ü© d s = ‚àû), d
set_option linter.uppercaseLean3 false in
#align dimH dimH

/-!
### Basic properties
-/


section Measurable

variable [MeasurableSpace X] [BorelSpace X]

/-- Unfold the definition of `dimH` using `[MeasurableSpace X] [BorelSpace X]` from the
environment. -/
theorem dimH_def (s : Set X) : dimH s = ‚®Ü (d : ‚Ñù‚â•0) (_ : ŒºH[d] s = ‚àû), (d : ‚Ñù‚â•0‚àû) := by
  borelize X; rw [dimH]
set_option linter.uppercaseLean3 false in
#align dimH_def dimH_def

theorem hausdorffMeasure_of_lt_dimH {s : Set X} {d : ‚Ñù‚â•0} (h : ‚Üëd < dimH s) : ŒºH[d] s = ‚àû := by
  simp only [dimH_def, lt_iSup_iff] at h
  rcases h with ‚ü®d', hsd', hdd'‚ü©
  rw [ENNReal.coe_lt_coe, ‚Üê NNReal.coe_lt_coe] at hdd'
  exact top_unique (hsd' ‚ñ∏ hausdorffMeasure_mono hdd'.le _)
set_option linter.uppercaseLean3 false in
#align hausdorff_measure_of_lt_dimH hausdorffMeasure_of_lt_dimH

theorem dimH_le {s : Set X} {d : ‚Ñù‚â•0‚àû} (H : ‚àÄ d' : ‚Ñù‚â•0, ŒºH[d'] s = ‚àû ‚Üí ‚Üëd' ‚â§ d) : dimH s ‚â§ d :=
  (dimH_def s).trans_le <| iSup‚ÇÇ_le H
set_option linter.uppercaseLean3 false in
#align dimH_le dimH_le

theorem dimH_le_of_hausdorffMeasure_ne_top {s : Set X} {d : ‚Ñù‚â•0} (h : ŒºH[d] s ‚â† ‚àû) : dimH s ‚â§ d :=
  le_of_not_lt <| mt hausdorffMeasure_of_lt_dimH h
set_option linter.uppercaseLean3 false in
#align dimH_le_of_hausdorff_measure_ne_top dimH_le_of_hausdorffMeasure_ne_top

theorem le_dimH_of_hausdorffMeasure_eq_top {s : Set X} {d : ‚Ñù‚â•0} (h : ŒºH[d] s = ‚àû) :
    ‚Üëd ‚â§ dimH s := by
  rw [dimH_def]; exact le_iSup‚ÇÇ (Œ± := ‚Ñù‚â•0‚àû) d h
set_option linter.uppercaseLean3 false in
#align le_dimH_of_hausdorff_measure_eq_top le_dimH_of_hausdorffMeasure_eq_top

theorem hausdorffMeasure_of_dimH_lt {s : Set X} {d : ‚Ñù‚â•0} (h : dimH s < d) : ŒºH[d] s = 0 := by
  rw [dimH_def] at h
  rcases ENNReal.lt_iff_exists_nnreal_btwn.1 h with ‚ü®d', hsd', hd'd‚ü©
  rw [ENNReal.coe_lt_coe, ‚Üê NNReal.coe_lt_coe] at hd'd
  exact (hausdorffMeasure_zero_or_top hd'd s).resolve_right fun h‚ÇÇ => hsd'.not_le <|
    le_iSup‚ÇÇ (Œ± := ‚Ñù‚â•0‚àû) d' h‚ÇÇ
set_option linter.uppercaseLean3 false in
#align hausdorff_measure_of_dimH_lt hausdorffMeasure_of_dimH_lt

theorem measure_zero_of_dimH_lt {Œº : Measure X} {d : ‚Ñù‚â•0} (h : Œº ‚â™ ŒºH[d]) {s : Set X}
    (hd : dimH s < d) : Œº s = 0 :=
  h <| hausdorffMeasure_of_dimH_lt hd
set_option linter.uppercaseLean3 false in
#align measure_zero_of_dimH_lt measure_zero_of_dimH_lt

theorem le_dimH_of_hausdorffMeasure_ne_zero {s : Set X} {d : ‚Ñù‚â•0} (h : ŒºH[d] s ‚â† 0) : ‚Üëd ‚â§ dimH s :=
  le_of_not_lt <| mt hausdorffMeasure_of_dimH_lt h
set_option linter.uppercaseLean3 false in
#align le_dimH_of_hausdorff_measure_ne_zero le_dimH_of_hausdorffMeasure_ne_zero

theorem dimH_of_hausdorffMeasure_ne_zero_ne_top {d : ‚Ñù‚â•0} {s : Set X} (h : ŒºH[d] s ‚â† 0)
    (h' : ŒºH[d] s ‚â† ‚àû) : dimH s = d :=
  le_antisymm (dimH_le_of_hausdorffMeasure_ne_top h') (le_dimH_of_hausdorffMeasure_ne_zero h)
set_option linter.uppercaseLean3 false in
#align dimH_of_hausdorff_measure_ne_zero_ne_top dimH_of_hausdorffMeasure_ne_zero_ne_top

end Measurable

@[mono]
theorem dimH_mono {s t : Set X} (h : s ‚äÜ t) : dimH s ‚â§ dimH t := by
  borelize X
  exact dimH_le fun d hd => le_dimH_of_hausdorffMeasure_eq_top <| top_unique <| hd ‚ñ∏ measure_mono h
set_option linter.uppercaseLean3 false in
#align dimH_mono dimH_mono

theorem dimH_subsingleton {s : Set X} (h : s.Subsingleton) : dimH s = 0 := by
  borelize X
  apply le_antisymm _ (zero_le _)
  refine dimH_le_of_hausdorffMeasure_ne_top ?_
  exact ((hausdorffMeasure_le_one_of_subsingleton h le_rfl).trans_lt ENNReal.one_lt_top).ne
set_option linter.uppercaseLean3 false in
#align dimH_subsingleton dimH_subsingleton

alias Set.Subsingleton.dimH_zero := dimH_subsingleton
set_option linter.uppercaseLean3 false in
#align set.subsingleton.dimH_zero Set.Subsingleton.dimH_zero

@[simp]
theorem dimH_empty : dimH (‚àÖ : Set X) = 0 :=
  subsingleton_empty.dimH_zero
set_option linter.uppercaseLean3 false in
#align dimH_empty dimH_empty

@[simp]
theorem dimH_singleton (x : X) : dimH ({x} : Set X) = 0 :=
  subsingleton_singleton.dimH_zero
set_option linter.uppercaseLean3 false in
#align dimH_singleton dimH_singleton

@[simp]
theorem dimH_iUnion {Œπ : Sort*} [Countable Œπ] (s : Œπ ‚Üí Set X) :
    dimH (‚ãÉ i, s i) = ‚®Ü i, dimH (s i) := by
  borelize X
  refine le_antisymm (dimH_le fun d hd => ?_) (iSup_le fun i => dimH_mono <| subset_iUnion _ _)
  contrapose! hd
  have : ‚àÄ i, ŒºH[d] (s i) = 0 := fun i =>
    hausdorffMeasure_of_dimH_lt ((le_iSup (fun i => dimH (s i)) i).trans_lt hd)
  rw [measure_iUnion_null this]
  exact ENNReal.zero_ne_top
set_option linter.uppercaseLean3 false in
#align dimH_Union dimH_iUnion

@[simp]
theorem dimH_bUnion {s : Set Œπ} (hs : s.Countable) (t : Œπ ‚Üí Set X) :
    dimH (‚ãÉ i ‚àà s, t i) = ‚®Ü i ‚àà s, dimH (t i) := by
  haveI := hs.toEncodable
  rw [biUnion_eq_iUnion, dimH_iUnion, ‚Üê iSup_subtype'']
set_option linter.uppercaseLean3 false in
#align dimH_bUnion dimH_bUnion

@[simp]
theorem dimH_sUnion {S : Set (Set X)} (hS : S.Countable) : dimH (‚ãÉ‚ÇÄ S) = ‚®Ü s ‚àà S, dimH s := by
  rw [sUnion_eq_biUnion, dimH_bUnion hS]
set_option linter.uppercaseLean3 false in
#align dimH_sUnion dimH_sUnion

@[simp]
theorem dimH_union (s t : Set X) : dimH (s ‚à™ t) = max (dimH s) (dimH t) := by
  rw [union_eq_iUnion, dimH_iUnion, iSup_bool_eq, cond, cond, ENNReal.sup_eq_max]
set_option linter.uppercaseLean3 false in
#align dimH_union dimH_union

theorem dimH_countable {s : Set X} (hs : s.Countable) : dimH s = 0 :=
  biUnion_of_singleton s ‚ñ∏ by simp only [dimH_bUnion hs, dimH_singleton, ENNReal.iSup_zero_eq_zero]
set_option linter.uppercaseLean3 false in
#align dimH_countable dimH_countable

alias Set.Countable.dimH_zero := dimH_countable
set_option linter.uppercaseLean3 false in
#align set.countable.dimH_zero Set.Countable.dimH_zero

theorem dimH_finite {s : Set X} (hs : s.Finite) : dimH s = 0 :=
  hs.countable.dimH_zero
set_option linter.uppercaseLean3 false in
#align dimH_finite dimH_finite

alias Set.Finite.dimH_zero := dimH_finite
set_option linter.uppercaseLean3 false in
#align set.finite.dimH_zero Set.Finite.dimH_zero

@[simp]
theorem dimH_coe_finset (s : Finset X) : dimH (s : Set X) = 0 :=
  s.finite_toSet.dimH_zero
set_option linter.uppercaseLean3 false in
#align dimH_coe_finset dimH_coe_finset

alias Finset.dimH_zero := dimH_coe_finset
set_option linter.uppercaseLean3 false in
#align finset.dimH_zero Finset.dimH_zero

/-!
### Hausdorff dimension as the supremum of local Hausdorff dimensions
-/


section

variable [SecondCountableTopology X]

/-- If `r` is less than the Hausdorff dimension of a set `s` in an (extended) metric space with
second countable topology, then there exists a point `x ‚àà s` such that every neighborhood
`t` of `x` within `s` has Hausdorff dimension greater than `r`. -/
theorem exists_mem_nhdsWithin_lt_dimH_of_lt_dimH {s : Set X} {r : ‚Ñù‚â•0‚àû} (h : r < dimH s) :
    ‚àÉ x ‚àà s, ‚àÄ t ‚àà ùìù[s] x, r < dimH t := by
  contrapose! h; choose! t htx htr using h
  rcases countable_cover_nhdsWithin htx with ‚ü®S, hSs, hSc, hSU‚ü©
  calc
    dimH s ‚â§ dimH (‚ãÉ x ‚àà S, t x) := dimH_mono hSU
    _ = ‚®Ü x ‚àà S, dimH (t x) := dimH_bUnion hSc _
    _ ‚â§ r := iSup‚ÇÇ_le fun x hx => htr x <| hSs hx
set_option linter.uppercaseLean3 false in
#align exists_mem_nhds_within_lt_dimH_of_lt_dimH exists_mem_nhdsWithin_lt_dimH_of_lt_dimH

/-- In an (extended) metric space with second countable topology, the Hausdorff dimension
of a set `s` is the supremum over `x ‚àà s` of the limit superiors of `dimH t` along
`(ùìù[s] x).smallSets`. -/
theorem bsupr_limsup_dimH (s : Set X) : ‚®Ü x ‚àà s, limsup dimH (ùìù[s] x).smallSets = dimH s := by
  refine le_antisymm (iSup‚ÇÇ_le fun x _ => ?_) ?_
  ¬∑ refine limsup_le_of_le isCobounded_le_of_bot ?_
    exact eventually_smallSets.2 ‚ü®s, self_mem_nhdsWithin, fun t => dimH_mono‚ü©
  ¬∑ refine le_of_forall_ge_of_dense fun r hr => ?_
    rcases exists_mem_nhdsWithin_lt_dimH_of_lt_dimH hr with ‚ü®x, hxs, hxr‚ü©
    refine le_iSup‚ÇÇ_of_le x hxs ?_; rw [limsup_eq]; refine le_sInf fun b hb => ?_
    rcases eventually_smallSets.1 hb with ‚ü®t, htx, ht‚ü©
    exact (hxr t htx).le.trans (ht t Subset.rfl)
set_option linter.uppercaseLean3 false in
#align bsupr_limsup_dimH bsupr_limsup_dimH

/-- In an (extended) metric space with second countable topology, the Hausdorff dimension
of a set `s` is the supremum over all `x` of the limit superiors of `dimH t` along
`(ùìù[s] x).smallSets`. -/
theorem iSup_limsup_dimH (s : Set X) : ‚®Ü x, limsup dimH (ùìù[s] x).smallSets = dimH s := by
  refine le_antisymm (iSup_le fun x => ?_) ?_
  ¬∑ refine limsup_le_of_le isCobounded_le_of_bot ?_
    exact eventually_smallSets.2 ‚ü®s, self_mem_nhdsWithin, fun t => dimH_mono‚ü©
  ¬∑ rw [‚Üê bsupr_limsup_dimH]; exact iSup‚ÇÇ_le_iSup _ _
set_option linter.uppercaseLean3 false in
#align supr_limsup_dimH iSup_limsup_dimH

end

/-!
### Hausdorff dimension and H√∂lder continuity
-/


variable {C K r : ‚Ñù‚â•0} {f : X ‚Üí Y} {s t : Set X}

/-- If `f` is a H√∂lder continuous map with exponent `r > 0`, then `dimH (f '' s) ‚â§ dimH s / r`. -/
theorem HolderOnWith.dimH_image_le (h : HolderOnWith C r f s) (hr : 0 < r) :
    dimH (f '' s) ‚â§ dimH s / r := by
  borelize X Y
  refine dimH_le fun d hd => ?_
  have := h.hausdorffMeasure_image_le hr d.coe_nonneg
  rw [hd, ENNReal.coe_rpow_of_nonneg _ d.coe_nonneg, top_le_iff] at this
  have Hrd : ŒºH[(r * d : ‚Ñù‚â•0)] s = ‚ä§ := by
    contrapose this
    exact ENNReal.mul_ne_top ENNReal.coe_ne_top this
  rw [ENNReal.le_div_iff_mul_le, mul_comm, ‚Üê ENNReal.coe_mul]
  exacts [le_dimH_of_hausdorffMeasure_eq_top Hrd, Or.inl (mt ENNReal.coe_eq_zero.1 hr.ne'),
    Or.inl ENNReal.coe_ne_top]
set_option linter.uppercaseLean3 false in
#align holder_on_with.dimH_image_le HolderOnWith.dimH_image_le

namespace HolderWith

/-- If `f : X ‚Üí Y` is H√∂lder continuous with a positive exponent `r`, then the Hausdorff dimension
of the image of a set `s` is at most `dimH s / r`. -/
theorem dimH_image_le (h : HolderWith C r f) (hr : 0 < r) (s : Set X) :
    dimH (f '' s) ‚â§ dimH s / r :=
  (h.holderOnWith s).dimH_image_le hr
set_option linter.uppercaseLean3 false in
#align holder_with.dimH_image_le HolderWith.dimH_image_le

/-- If `f` is a H√∂lder continuous map with exponent `r > 0`, then the Hausdorff dimension of its
range is at most the Hausdorff dimension of its domain divided by `r`. -/
theorem dimH_range_le (h : HolderWith C r f) (hr : 0 < r) :
    dimH (range f) ‚â§ dimH (univ : Set X) / r :=
  @image_univ _ _ f ‚ñ∏ h.dimH_image_le hr univ
set_option linter.uppercaseLean3 false in
#align holder_with.dimH_range_le HolderWith.dimH_range_le

end HolderWith

/-- If `s` is a set in a space `X` with second countable topology and `f : X ‚Üí Y` is H√∂lder
continuous in a neighborhood within `s` of every point `x ‚àà s` with the same positive exponent `r`
but possibly different coefficients, then the Hausdorff dimension of the image `f '' s` is at most
the Hausdorff dimension of `s` divided by `r`. -/
theorem dimH_image_le_of_locally_holder_on [SecondCountableTopology X] {r : ‚Ñù‚â•0} {f : X ‚Üí Y}
    (hr : 0 < r) {s : Set X} (hf : ‚àÄ x ‚àà s, ‚àÉ C : ‚Ñù‚â•0, ‚àÉ t ‚àà ùìù[s] x, HolderOnWith C r f t) :
    dimH (f '' s) ‚â§ dimH s / r := by
  choose! C t htn hC using hf
  rcases countable_cover_nhdsWithin htn with ‚ü®u, hus, huc, huU‚ü©
  replace huU := inter_eq_self_of_subset_left huU; rw [inter_iUnion‚ÇÇ] at huU
  rw [‚Üê huU, image_iUnion‚ÇÇ, dimH_bUnion huc, dimH_bUnion huc]; simp only [ENNReal.iSup_div]
  exact iSup‚ÇÇ_mono fun x hx => ((hC x (hus hx)).mono inter_subset_right).dimH_image_le hr
set_option linter.uppercaseLean3 false in
#align dimH_image_le_of_locally_holder_on dimH_image_le_of_locally_holder_on

/-- If `f : X ‚Üí Y` is H√∂lder continuous in a neighborhood of every point `x : X` with the same
positive exponent `r` but possibly different coefficients, then the Hausdorff dimension of the range
of `f` is at most the Hausdorff dimension of `X` divided by `r`. -/
theorem dimH_range_le_of_locally_holder_on [SecondCountableTopology X] {r : ‚Ñù‚â•0} {f : X ‚Üí Y}
    (hr : 0 < r) (hf : ‚àÄ x : X, ‚àÉ C : ‚Ñù‚â•0, ‚àÉ s ‚àà ùìù x, HolderOnWith C r f s) :
    dimH (range f) ‚â§ dimH (univ : Set X) / r := by
  rw [‚Üê image_univ]
  refine dimH_image_le_of_locally_holder_on hr fun x _ => ?_
  simpa only [exists_prop, nhdsWithin_univ] using hf x
set_option linter.uppercaseLean3 false in
#align dimH_range_le_of_locally_holder_on dimH_range_le_of_locally_holder_on

/-!
### Hausdorff dimension and Lipschitz continuity
-/


/-- If `f : X ‚Üí Y` is Lipschitz continuous on `s`, then `dimH (f '' s) ‚â§ dimH s`. -/
theorem LipschitzOnWith.dimH_image_le (h : LipschitzOnWith K f s) : dimH (f '' s) ‚â§ dimH s := by
  simpa using h.holderOnWith.dimH_image_le zero_lt_one
set_option linter.uppercaseLean3 false in
#align lipschitz_on_with.dimH_image_le LipschitzOnWith.dimH_image_le

namespace LipschitzWith

/-- If `f` is a Lipschitz continuous map, then `dimH (f '' s) ‚â§ dimH s`. -/
theorem dimH_image_le (h : LipschitzWith K f) (s : Set X) : dimH (f '' s) ‚â§ dimH s :=
  (h.lipschitzOnWith s).dimH_image_le
set_option linter.uppercaseLean3 false in
#align lipschitz_with.dimH_image_le LipschitzWith.dimH_image_le

/-- If `f` is a Lipschitz continuous map, then the Hausdorff dimension of its range is at most the
Hausdorff dimension of its domain. -/
theorem dimH_range_le (h : LipschitzWith K f) : dimH (range f) ‚â§ dimH (univ : Set X) :=
  @image_univ _ _ f ‚ñ∏ h.dimH_image_le univ
set_option linter.uppercaseLean3 false in
#align lipschitz_with.dimH_range_le LipschitzWith.dimH_range_le

end LipschitzWith

/-- If `s` is a set in an extended metric space `X` with second countable topology and `f : X ‚Üí Y`
is Lipschitz in a neighborhood within `s` of every point `x ‚àà s`, then the Hausdorff dimension of
the image `f '' s` is at most the Hausdorff dimension of `s`. -/
theorem dimH_image_le_of_locally_lipschitzOn [SecondCountableTopology X] {f : X ‚Üí Y} {s : Set X}
    (hf : ‚àÄ x ‚àà s, ‚àÉ C : ‚Ñù‚â•0, ‚àÉ t ‚àà ùìù[s] x, LipschitzOnWith C f t) : dimH (f '' s) ‚â§ dimH s := by
  have : ‚àÄ x ‚àà s, ‚àÉ C : ‚Ñù‚â•0, ‚àÉ t ‚àà ùìù[s] x, HolderOnWith C 1 f t := by
    simpa only [holderOnWith_one] using hf
  simpa only [ENNReal.coe_one, div_one] using dimH_image_le_of_locally_holder_on zero_lt_one this
set_option linter.uppercaseLean3 false in
#align dimH_image_le_of_locally_lipschitz_on dimH_image_le_of_locally_lipschitzOn

/-- If `f : X ‚Üí Y` is Lipschitz in a neighborhood of each point `x : X`, then the Hausdorff
dimension of `range f` is at most the Hausdorff dimension of `X`. -/
theorem dimH_range_le_of_locally_lipschitzOn [SecondCountableTopology X] {f : X ‚Üí Y}
    (hf : ‚àÄ x : X, ‚àÉ C : ‚Ñù‚â•0, ‚àÉ s ‚àà ùìù x, LipschitzOnWith C f s) :
    dimH (range f) ‚â§ dimH (univ : Set X) := by
  rw [‚Üê image_univ]
  refine dimH_image_le_of_locally_lipschitzOn fun x _ => ?_
  simpa only [exists_prop, nhdsWithin_univ] using hf x
set_option linter.uppercaseLean3 false in
#align dimH_range_le_of_locally_lipschitz_on dimH_range_le_of_locally_lipschitzOn

namespace AntilipschitzWith

theorem dimH_preimage_le (hf : AntilipschitzWith K f) (s : Set Y) : dimH (f ‚Åª¬π' s) ‚â§ dimH s := by
  borelize X Y
  refine dimH_le fun d hd => le_dimH_of_hausdorffMeasure_eq_top ?_
  have := hf.hausdorffMeasure_preimage_le d.coe_nonneg s
  rw [hd, top_le_iff] at this
  contrapose! this
  exact ENNReal.mul_ne_top (by simp) this
set_option linter.uppercaseLean3 false in
#align antilipschitz_with.dimH_preimage_le AntilipschitzWith.dimH_preimage_le

theorem le_dimH_image (hf : AntilipschitzWith K f) (s : Set X) : dimH s ‚â§ dimH (f '' s) :=
  calc
    dimH s ‚â§ dimH (f ‚Åª¬π' (f '' s)) := dimH_mono (subset_preimage_image _ _)
    _ ‚â§ dimH (f '' s) := hf.dimH_preimage_le _
set_option linter.uppercaseLean3 false in
#align antilipschitz_with.le_dimH_image AntilipschitzWith.le_dimH_image

end AntilipschitzWith

/-!
### Isometries preserve Hausdorff dimension
-/


theorem Isometry.dimH_image (hf : Isometry f) (s : Set X) : dimH (f '' s) = dimH s :=
  le_antisymm (hf.lipschitz.dimH_image_le _) (hf.antilipschitz.le_dimH_image _)
set_option linter.uppercaseLean3 false in
#align isometry.dimH_image Isometry.dimH_image

namespace IsometryEquiv

@[simp]
theorem dimH_image (e : X ‚âÉ·µ¢ Y) (s : Set X) : dimH (e '' s) = dimH s :=
  e.isometry.dimH_image s
set_option linter.uppercaseLean3 false in
#align isometry_equiv.dimH_image IsometryEquiv.dimH_image

@[simp]
theorem dimH_preimage (e : X ‚âÉ·µ¢ Y) (s : Set Y) : dimH (e ‚Åª¬π' s) = dimH s := by
  rw [‚Üê e.image_symm, e.symm.dimH_image]
set_option linter.uppercaseLean3 false in
#align isometry_equiv.dimH_preimage IsometryEquiv.dimH_preimage

theorem dimH_univ (e : X ‚âÉ·µ¢ Y) : dimH (univ : Set X) = dimH (univ : Set Y) := by
  rw [‚Üê e.dimH_preimage univ, preimage_univ]
set_option linter.uppercaseLean3 false in
#align isometry_equiv.dimH_univ IsometryEquiv.dimH_univ

end IsometryEquiv

namespace ContinuousLinearEquiv

variable {ùïú E F : Type*} [NontriviallyNormedField ùïú] [NormedAddCommGroup E] [NormedSpace ùïú E]
  [NormedAddCommGroup F] [NormedSpace ùïú F]

@[simp]
theorem dimH_image (e : E ‚âÉL[ùïú] F) (s : Set E) : dimH (e '' s) = dimH s :=
  le_antisymm (e.lipschitz.dimH_image_le s) <| by
    simpa only [e.symm_image_image] using e.symm.lipschitz.dimH_image_le (e '' s)
set_option linter.uppercaseLean3 false in
#align continuous_linear_equiv.dimH_image ContinuousLinearEquiv.dimH_image

@[simp]
theorem dimH_preimage (e : E ‚âÉL[ùïú] F) (s : Set F) : dimH (e ‚Åª¬π' s) = dimH s := by
  rw [‚Üê e.image_symm_eq_preimage, e.symm.dimH_image]
set_option linter.uppercaseLean3 false in
#align continuous_linear_equiv.dimH_preimage ContinuousLinearEquiv.dimH_preimage

theorem dimH_univ (e : E ‚âÉL[ùïú] F) : dimH (univ : Set E) = dimH (univ : Set F) := by
  rw [‚Üê e.dimH_preimage, preimage_univ]
set_option linter.uppercaseLean3 false in
#align continuous_linear_equiv.dimH_univ ContinuousLinearEquiv.dimH_univ

end ContinuousLinearEquiv

/-!
### Hausdorff dimension in a real vector space
-/


namespace Real

variable {E : Type*} [Fintype Œπ] [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [FiniteDimensional ‚Ñù E]

theorem dimH_ball_pi (x : Œπ ‚Üí ‚Ñù) {r : ‚Ñù} (hr : 0 < r) :
    dimH (Metric.ball x r) = Fintype.card Œπ := by
  cases isEmpty_or_nonempty Œπ
  ¬∑ rwa [dimH_subsingleton, eq_comm, Nat.cast_eq_zero, Fintype.card_eq_zero_iff]
    exact fun x _ y _ => Subsingleton.elim x y
  ¬∑ rw [‚Üê ENNReal.coe_natCast]
    have : ŒºH[Fintype.card Œπ] (Metric.ball x r) = ENNReal.ofReal ((2 * r) ^ Fintype.card Œπ) := by
      rw [hausdorffMeasure_pi_real, Real.volume_pi_ball _ hr]
    refine dimH_of_hausdorffMeasure_ne_zero_ne_top ?_ ?_ <;> rw [NNReal.coe_natCast, this]
    ¬∑ simp [pow_pos (mul_pos (zero_lt_two' ‚Ñù) hr)]
    ¬∑ exact ENNReal.ofReal_ne_top
set_option linter.uppercaseLean3 false in
#align real.dimH_ball_pi Real.dimH_ball_pi

theorem dimH_ball_pi_fin {n : ‚Ñï} (x : Fin n ‚Üí ‚Ñù) {r : ‚Ñù} (hr : 0 < r) :
    dimH (Metric.ball x r) = n := by rw [dimH_ball_pi x hr, Fintype.card_fin]
set_option linter.uppercaseLean3 false in
#align real.dimH_ball_pi_fin Real.dimH_ball_pi_fin

theorem dimH_univ_pi (Œπ : Type*) [Fintype Œπ] : dimH (univ : Set (Œπ ‚Üí ‚Ñù)) = Fintype.card Œπ := by
  simp only [‚Üê Metric.iUnion_ball_nat_succ (0 : Œπ ‚Üí ‚Ñù), dimH_iUnion,
    dimH_ball_pi _ (Nat.cast_add_one_pos _), iSup_const]
set_option linter.uppercaseLean3 false in
#align real.dimH_univ_pi Real.dimH_univ_pi

theorem dimH_univ_pi_fin (n : ‚Ñï) : dimH (univ : Set (Fin n ‚Üí ‚Ñù)) = n := by
  rw [dimH_univ_pi, Fintype.card_fin]
set_option linter.uppercaseLean3 false in
#align real.dimH_univ_pi_fin Real.dimH_univ_pi_fin

theorem dimH_of_mem_nhds {x : E} {s : Set E} (h : s ‚àà ùìù x) : dimH s = finrank ‚Ñù E := by
  have e : E ‚âÉL[‚Ñù] Fin (finrank ‚Ñù E) ‚Üí ‚Ñù :=
    ContinuousLinearEquiv.ofFinrankEq (FiniteDimensional.finrank_fin_fun ‚Ñù).symm
  rw [‚Üê e.dimH_image]
  refine le_antisymm ?_ ?_
  ¬∑ exact (dimH_mono (subset_univ _)).trans_eq (dimH_univ_pi_fin _)
  ¬∑ have : e '' s ‚àà ùìù (e x) := by rw [‚Üê e.map_nhds_eq]; exact image_mem_map h
    rcases Metric.nhds_basis_ball.mem_iff.1 this with ‚ü®r, hr0, hr‚ü©
    simpa only [dimH_ball_pi_fin (e x) hr0] using dimH_mono hr
set_option linter.uppercaseLean3 false in
#align real.dimH_of_mem_nhds Real.dimH_of_mem_nhds

theorem dimH_of_nonempty_interior {s : Set E} (h : (interior s).Nonempty) : dimH s = finrank ‚Ñù E :=
  let ‚ü®_, hx‚ü© := h
  dimH_of_mem_nhds (mem_interior_iff_mem_nhds.1 hx)
set_option linter.uppercaseLean3 false in
#align real.dimH_of_nonempty_interior Real.dimH_of_nonempty_interior

variable (E)

theorem dimH_univ_eq_finrank : dimH (univ : Set E) = finrank ‚Ñù E :=
  dimH_of_mem_nhds (@univ_mem _ (ùìù 0))
set_option linter.uppercaseLean3 false in
#align real.dimH_univ_eq_finrank Real.dimH_univ_eq_finrank

theorem dimH_univ : dimH (univ : Set ‚Ñù) = 1 := by
  rw [dimH_univ_eq_finrank ‚Ñù, FiniteDimensional.finrank_self, Nat.cast_one]
set_option linter.uppercaseLean3 false in
#align real.dimH_univ Real.dimH_univ

variable {E}

lemma hausdorffMeasure_of_finrank_lt [MeasurableSpace E] [BorelSpace E] {d : ‚Ñù}
    (hd : finrank ‚Ñù E < d) : (ŒºH[d] : Measure E) = 0 := by
  lift d to ‚Ñù‚â•0 using (Nat.cast_nonneg _).trans hd.le
  rw [‚Üê measure_univ_eq_zero]
  apply hausdorffMeasure_of_dimH_lt
  rw [dimH_univ_eq_finrank]
  exact mod_cast hd

end Real

variable {E F : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [FiniteDimensional ‚Ñù E]
  [NormedAddCommGroup F] [NormedSpace ‚Ñù F]

theorem dense_compl_of_dimH_lt_finrank {s : Set E} (hs : dimH s < finrank ‚Ñù E) : Dense s·∂ú := by
  refine fun x => mem_closure_iff_nhds.2 fun t ht => nonempty_iff_ne_empty.2 fun he => hs.not_le ?_
  rw [‚Üê diff_eq, diff_eq_empty] at he
  rw [‚Üê Real.dimH_of_mem_nhds ht]
  exact dimH_mono he
set_option linter.uppercaseLean3 false in
#align dense_compl_of_dimH_lt_finrank dense_compl_of_dimH_lt_finrank

/-!
### Hausdorff dimension and `C¬π`-smooth maps

`C¬π`-smooth maps are locally Lipschitz continuous, hence they do not increase the Hausdorff
dimension of sets.
-/


/-- Let `f` be a function defined on a finite dimensional real normed space. If `f` is `C¬π`-smooth
on a convex set `s`, then the Hausdorff dimension of `f '' s` is less than or equal to the Hausdorff
dimension of `s`.

TODO: do we actually need `Convex ‚Ñù s`? -/
theorem ContDiffOn.dimH_image_le {f : E ‚Üí F} {s t : Set E} (hf : ContDiffOn ‚Ñù 1 f s)
    (hc : Convex ‚Ñù s) (ht : t ‚äÜ s) : dimH (f '' t) ‚â§ dimH t :=
  dimH_image_le_of_locally_lipschitzOn fun x hx =>
    let ‚ü®C, u, hu, hf‚ü© := (hf x (ht hx)).exists_lipschitzOnWith hc
    ‚ü®C, u, nhdsWithin_mono _ ht hu, hf‚ü©
set_option linter.uppercaseLean3 false in
#align cont_diff_on.dimH_image_le ContDiffOn.dimH_image_le

/-- The Hausdorff dimension of the range of a `C¬π`-smooth function defined on a finite dimensional
real normed space is at most the dimension of its domain as a vector space over `‚Ñù`. -/
theorem ContDiff.dimH_range_le {f : E ‚Üí F} (h : ContDiff ‚Ñù 1 f) : dimH (range f) ‚â§ finrank ‚Ñù E :=
  calc
    dimH (range f) = dimH (f '' univ) := by rw [image_univ]
    _ ‚â§ dimH (univ : Set E) := h.contDiffOn.dimH_image_le convex_univ Subset.rfl
    _ = finrank ‚Ñù E := Real.dimH_univ_eq_finrank E
set_option linter.uppercaseLean3 false in
#align cont_diff.dimH_range_le ContDiff.dimH_range_le

/-- A particular case of Sard's Theorem. Let `f : E ‚Üí F` be a map between finite dimensional real
vector spaces. Suppose that `f` is `C¬π` smooth on a convex set `s` of Hausdorff dimension strictly
less than the dimension of `F`. Then the complement of the image `f '' s` is dense in `F`. -/
theorem ContDiffOn.dense_compl_image_of_dimH_lt_finrank [FiniteDimensional ‚Ñù F] {f : E ‚Üí F}
    {s t : Set E} (h : ContDiffOn ‚Ñù 1 f s) (hc : Convex ‚Ñù s) (ht : t ‚äÜ s)
    (htF : dimH t < finrank ‚Ñù F) : Dense (f '' t)·∂ú :=
  dense_compl_of_dimH_lt_finrank <| (h.dimH_image_le hc ht).trans_lt htF
set_option linter.uppercaseLean3 false in
#align cont_diff_on.dense_compl_image_of_dimH_lt_finrank ContDiffOn.dense_compl_image_of_dimH_lt_finrank

/-- A particular case of Sard's Theorem. If `f` is a `C¬π` smooth map from a real vector space to a
real vector space `F` of strictly larger dimension, then the complement of the range of `f` is dense
in `F`. -/
theorem ContDiff.dense_compl_range_of_finrank_lt_finrank [FiniteDimensional ‚Ñù F] {f : E ‚Üí F}
    (h : ContDiff ‚Ñù 1 f) (hEF : finrank ‚Ñù E < finrank ‚Ñù F) : Dense (range f)·∂ú :=
  dense_compl_of_dimH_lt_finrank <| h.dimH_range_le.trans_lt <| Nat.cast_lt.2 hEF
#align cont_diff.dense_compl_range_of_finrank_lt_finrank ContDiff.dense_compl_range_of_finrank_lt_finrank
