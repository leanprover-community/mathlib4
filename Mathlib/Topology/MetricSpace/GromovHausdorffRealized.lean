/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Topology.MetricSpace.Gluing
import Mathlib.Topology.MetricSpace.HausdorffDistance
import Mathlib.Topology.ContinuousFunction.Bounded

#align_import topology.metric_space.gromov_hausdorff_realized from "leanprover-community/mathlib"@"0c1f285a9f6e608ae2bdffa3f993eafb01eba829"

/-!
# The Gromov-Hausdorff distance is realized

In this file, we construct of a good coupling between nonempty compact metric spaces, minimizing
their Hausdorff distance. This construction is instrumental to study the Gromov-Hausdorff
distance between nonempty compact metric spaces.

Given two nonempty compact metric spaces `X` and `Y`, we define `OptimalGHCoupling X Y` as a
compact metric space, together with two isometric embeddings `optimalGHInjl` and `optimalGHInjr`
respectively of `X` and `Y` into `OptimalGHCoupling X Y`. The main property of the optimal
coupling is that the Hausdorff distance between `X` and `Y` in `OptimalGHCoupling X Y` is smaller
than the corresponding distance in any other coupling. We do not prove completely this fact in this
file, but we show a good enough approximation of this fact in `hausdorffDist_optimal_le_HD`, that
will suffice to obtain the full statement once the Gromov-Hausdorff distance is properly defined,
in `hausdorffDist_optimal`.

The key point in the construction is that the set of possible distances coming from isometric
embeddings of `X` and `Y` in metric spaces is a set of equicontinuous functions. By Arzela-Ascoli,
it is compact, and one can find such a distance which is minimal. This distance defines a premetric
space structure on `X ‚äï Y`. The corresponding metric quotient is `OptimalGHCoupling X Y`.
-/


noncomputable section

open scoped Classical
open Topology NNReal

universe u v w

open scoped Classical
open Set Function TopologicalSpace Filter Metric Quotient

open BoundedContinuousFunction

open Sum (inl inr)

attribute [local instance] metricSpaceSum

namespace GromovHausdorff

set_option linter.uppercaseLean3 false -- `Gromov_Hausdorff`

section GromovHausdorffRealized

/-! This section shows that the Gromov-Hausdorff distance
is realized. For this, we consider candidate distances on the disjoint union
`X ‚äï Y` of two compact nonempty metric spaces, almost realizing the Gromov-Hausdorff
distance, and show that they form a compact family by applying Arzela-Ascoli
theorem. The existence of a minimizer follows. -/
section Definitions

variable (X : Type u) (Y : Type v) [MetricSpace X] [CompactSpace X] [Nonempty X] [MetricSpace Y]
  [CompactSpace Y] [Nonempty Y]

private abbrev ProdSpaceFun : Type _ :=
  (X ‚äï Y) √ó (X ‚äï Y) ‚Üí ‚Ñù

private abbrev Cb : Type _ :=
  BoundedContinuousFunction ((X ‚äï Y) √ó (X ‚äï Y)) ‚Ñù

private def maxVar : ‚Ñù‚â•0 :=
  2 * ‚ü®diam (univ : Set X), diam_nonneg‚ü© + 1 + 2 * ‚ü®diam (univ : Set Y), diam_nonneg‚ü©

private theorem one_le_maxVar : 1 ‚â§ maxVar X Y :=
  calc
    (1 : Real) = 2 * 0 + 1 + 2 * 0 := by simp
    _ ‚â§ 2 * diam (univ : Set X) + 1 + 2 * diam (univ : Set Y) := by gcongr <;> positivity

/-- The set of functions on `X ‚äï Y` that are candidates distances to realize the
minimum of the Hausdorff distances between `X` and `Y` in a coupling. -/
def candidates : Set (ProdSpaceFun X Y) :=
  { f | (((((‚àÄ x y : X, f (Sum.inl x, Sum.inl y) = dist x y) ‚àß
      ‚àÄ x y : Y, f (Sum.inr x, Sum.inr y) = dist x y) ‚àß
      ‚àÄ x y, f (x, y) = f (y, x)) ‚àß
      ‚àÄ x y z, f (x, z) ‚â§ f (x, y) + f (y, z)) ‚àß
      ‚àÄ x, f (x, x) = 0) ‚àß
      ‚àÄ x y, f (x, y) ‚â§ maxVar X Y }
#align Gromov_Hausdorff.candidates GromovHausdorff.candidates

/-- Version of the set of candidates in bounded_continuous_functions, to apply Arzela-Ascoli. -/
private def candidatesB : Set (Cb X Y) :=
  { f : Cb X Y | (f : _ ‚Üí ‚Ñù) ‚àà candidates X Y }

end Definitions

section Constructions

variable {X : Type u} {Y : Type v} [MetricSpace X] [CompactSpace X] [Nonempty X] [MetricSpace Y]
  [CompactSpace Y] [Nonempty Y] {f : ProdSpaceFun X Y} {x y z t : X ‚äï Y}

attribute [local instance 10] Classical.inhabited_of_nonempty'

private theorem maxVar_bound : dist x y ‚â§ maxVar X Y :=
  calc
    dist x y ‚â§ diam (univ : Set (X ‚äï Y)) :=
      dist_le_diam_of_mem isBounded_of_compactSpace (mem_univ _) (mem_univ _)
    _ = diam (range inl ‚à™ range inr : Set (X ‚äï Y)) := by rw [range_inl_union_range_inr]
    _ ‚â§ diam (range inl : Set (X ‚äï Y)) + dist (inl default) (inr default) +
        diam (range inr : Set (X ‚äï Y)) :=
      (diam_union (mem_range_self _) (mem_range_self _))
    _ = diam (univ : Set X) + (dist default default + 1 + dist default default) +
        diam (univ : Set Y) := by
      rw [isometry_inl.diam_range, isometry_inr.diam_range]
      rfl
    _ = 1 * diam (univ : Set X) + 1 + 1 * diam (univ : Set Y) := by simp
    _ ‚â§ 2 * diam (univ : Set X) + 1 + 2 * diam (univ : Set Y) := by gcongr <;> norm_num

private theorem candidates_symm (fA : f ‚àà candidates X Y) : f (x, y) = f (y, x) :=
  fA.1.1.1.2 x y

private theorem candidates_triangle (fA : f ‚àà candidates X Y) : f (x, z) ‚â§ f (x, y) + f (y, z) :=
  fA.1.1.2 x y z

private theorem candidates_refl (fA : f ‚àà candidates X Y) : f (x, x) = 0 :=
  fA.1.2 x

private theorem candidates_nonneg (fA : f ‚àà candidates X Y) : 0 ‚â§ f (x, y) := by
  have : 0 ‚â§ 2 * f (x, y) :=
    calc
      0 = f (x, x) := (candidates_refl fA).symm
      _ ‚â§ f (x, y) + f (y, x) := candidates_triangle fA
      _ = f (x, y) + f (x, y) := by rw [candidates_symm fA]
      _ = 2 * f (x, y) := by ring
  linarith

private theorem candidates_dist_inl (fA : f ‚àà candidates X Y) (x y : X) :
    f (inl x, inl y) = dist x y :=
  fA.1.1.1.1.1 x y

private theorem candidates_dist_inr (fA : f ‚àà candidates X Y) (x y : Y) :
    f (inr x, inr y) = dist x y :=
  fA.1.1.1.1.2 x y

private theorem candidates_le_maxVar (fA : f ‚àà candidates X Y) : f (x, y) ‚â§ maxVar X Y :=
  fA.2 x y

/-- candidates are bounded by `maxVar X Y` -/
private theorem candidates_dist_bound (fA : f ‚àà candidates X Y) :
    ‚àÄ {x y : X ‚äï Y}, f (x, y) ‚â§ maxVar X Y * dist x y
  | inl x, inl y =>
    calc
      f (inl x, inl y) = dist x y := candidates_dist_inl fA x y
      _ = dist (inl x) (inl y) := by
        rw [@Sum.dist_eq X Y]
        rfl
      _ = 1 * dist (inl x) (inl y) := by ring
      _ ‚â§ maxVar X Y * dist (inl x) (inl y) := by gcongr; exact one_le_maxVar X Y
  | inl x, inr y =>
    calc
      f (inl x, inr y) ‚â§ maxVar X Y := candidates_le_maxVar fA
      _ = maxVar X Y * 1 := by simp
      _ ‚â§ maxVar X Y * dist (inl x) (inr y) := by gcongr; apply Sum.one_le_dist_inl_inr
  | inr x, inl y =>
    calc
      f (inr x, inl y) ‚â§ maxVar X Y := candidates_le_maxVar fA
      _ = maxVar X Y * 1 := by simp
      _ ‚â§ maxVar X Y * dist (inl x) (inr y) := by gcongr; apply Sum.one_le_dist_inl_inr
  | inr x, inr y =>
    calc
      f (inr x, inr y) = dist x y := candidates_dist_inr fA x y
      _ = dist (inr x) (inr y) := by
        rw [@Sum.dist_eq X Y]
        rfl
      _ = 1 * dist (inr x) (inr y) := by ring
      _ ‚â§ maxVar X Y * dist (inr x) (inr y) := by gcongr; exact one_le_maxVar X Y

/-- Technical lemma to prove that candidates are Lipschitz -/
private theorem candidates_lipschitz_aux (fA : f ‚àà candidates X Y) :
    f (x, y) - f (z, t) ‚â§ 2 * maxVar X Y * dist (x, y) (z, t) :=
  calc
    f (x, y) - f (z, t) ‚â§ f (x, t) + f (t, y) - f (z, t) := by gcongr; exact candidates_triangle fA
    _ ‚â§ f (x, z) + f (z, t) + f (t, y) - f (z, t) := by gcongr; exact candidates_triangle fA
    _ = f (x, z) + f (t, y) := by simp [sub_eq_add_neg, add_assoc]
    _ ‚â§ maxVar X Y * dist x z + maxVar X Y * dist t y := by
      gcongr <;> apply candidates_dist_bound fA
    _ ‚â§ maxVar X Y * max (dist x z) (dist t y) + maxVar X Y * max (dist x z) (dist t y) := by
      gcongr
      ¬∑ apply le_max_left
      ¬∑ apply le_max_right
    _ = 2 * maxVar X Y * max (dist x z) (dist y t) := by
      rw [dist_comm t y]
      ring
    _ = 2 * maxVar X Y * dist (x, y) (z, t) := rfl

/-- Candidates are Lipschitz -/
private theorem candidates_lipschitz (fA : f ‚àà candidates X Y) :
    LipschitzWith (2 * maxVar X Y) f := by
  apply LipschitzWith.of_dist_le_mul
  rintro ‚ü®x, y‚ü© ‚ü®z, t‚ü©
  rw [Real.dist_eq, abs_sub_le_iff]
  use candidates_lipschitz_aux fA
  rw [dist_comm]
  exact candidates_lipschitz_aux fA

/-- candidates give rise to elements of `BoundedContinuousFunction`s -/
def candidatesBOfCandidates (f : ProdSpaceFun X Y) (fA : f ‚àà candidates X Y) : Cb X Y :=
  BoundedContinuousFunction.mkOfCompact ‚ü®f, (candidates_lipschitz fA).continuous‚ü©
#align Gromov_Hausdorff.candidates_b_of_candidates GromovHausdorff.candidatesBOfCandidates

theorem candidatesBOfCandidates_mem (f : ProdSpaceFun X Y) (fA : f ‚àà candidates X Y) :
    candidatesBOfCandidates f fA ‚àà candidatesB X Y :=
  fA
#align Gromov_Hausdorff.candidates_b_of_candidates_mem GromovHausdorff.candidatesBOfCandidates_mem

/-- The distance on `X ‚äï Y` is a candidate -/
private theorem dist_mem_candidates :
    (fun p : (X ‚äï Y) √ó (X ‚äï Y) => dist p.1 p.2) ‚àà candidates X Y := by
  simp_rw [candidates, Set.mem_setOf_eq, dist_comm, dist_triangle, dist_self, maxVar_bound,
    forall_const, and_true]
  exact ‚ü®fun x y => rfl, fun x y => rfl‚ü©

/-- The distance on `X ‚äï Y` as a candidate -/
def candidatesBDist (X : Type u) (Y : Type v) [MetricSpace X] [CompactSpace X] [Nonempty X]
    [MetricSpace Y] [CompactSpace Y] [Nonempty Y] : Cb X Y :=
  candidatesBOfCandidates _ dist_mem_candidates
#align Gromov_Hausdorff.candidates_b_dist GromovHausdorff.candidatesBDist

theorem candidatesBDist_mem_candidatesB : candidatesBDist X Y ‚àà candidatesB X Y :=
  candidatesBOfCandidates_mem _ _
#align Gromov_Hausdorff.candidates_b_dist_mem_candidates_b GromovHausdorff.candidatesBDist_mem_candidatesB

private theorem candidatesB_nonempty : (candidatesB X Y).Nonempty :=
  ‚ü®_, candidatesBDist_mem_candidatesB‚ü©

/-- To apply Arzela-Ascoli, we need to check that the set of candidates is closed and
equicontinuous. Equicontinuity follows from the Lipschitz control, we check closedness. -/
private theorem closed_candidatesB : IsClosed (candidatesB X Y) := by
  have I1 : ‚àÄ x y, IsClosed { f : Cb X Y | f (inl x, inl y) = dist x y } := fun x y =>
    isClosed_eq continuous_eval_const continuous_const
  have I2 : ‚àÄ x y, IsClosed { f : Cb X Y | f (inr x, inr y) = dist x y } := fun x y =>
    isClosed_eq continuous_eval_const continuous_const
  have I3 : ‚àÄ x y, IsClosed { f : Cb X Y | f (x, y) = f (y, x) } := fun x y =>
    isClosed_eq continuous_eval_const continuous_eval_const
  have I4 : ‚àÄ x y z, IsClosed { f : Cb X Y | f (x, z) ‚â§ f (x, y) + f (y, z) } := fun x y z =>
    isClosed_le continuous_eval_const (continuous_eval_const.add continuous_eval_const)
  have I5 : ‚àÄ x, IsClosed { f : Cb X Y | f (x, x) = 0 } := fun x =>
    isClosed_eq continuous_eval_const continuous_const
  have I6 : ‚àÄ x y, IsClosed { f : Cb X Y | f (x, y) ‚â§ maxVar X Y } := fun x y =>
    isClosed_le continuous_eval_const continuous_const
  have : candidatesB X Y = (((((‚ãÇ (x) (y), { f : Cb X Y | f (@inl X Y x, @inl X Y y) = dist x y }) ‚à©
      ‚ãÇ (x) (y), { f : Cb X Y | f (@inr X Y x, @inr X Y y) = dist x y }) ‚à©
      ‚ãÇ (x) (y), { f : Cb X Y | f (x, y) = f (y, x) }) ‚à©
      ‚ãÇ (x) (y) (z), { f : Cb X Y | f (x, z) ‚â§ f (x, y) + f (y, z) }) ‚à©
      ‚ãÇ x, { f : Cb X Y | f (x, x) = 0 }) ‚à©
      ‚ãÇ (x) (y), { f : Cb X Y | f (x, y) ‚â§ maxVar X Y } := by
    ext
    simp only [candidatesB, candidates, mem_inter_iff, mem_iInter, mem_setOf_eq]
  rw [this]
  repeat'
    first
      |apply IsClosed.inter _ _
      |apply isClosed_iInter _
      |apply I1 _ _|apply I2 _ _|apply I3 _ _|apply I4 _ _ _|apply I5 _|apply I6 _ _|intro x

/-- Compactness of candidates (in `BoundedContinuousFunction`s) follows. -/
private theorem isCompact_candidatesB : IsCompact (candidatesB X Y) := by
  refine arzela_ascoli‚ÇÇ
      (Icc 0 (maxVar X Y) : Set ‚Ñù) isCompact_Icc (candidatesB X Y) closed_candidatesB ?_ ?_
  ¬∑ rintro f ‚ü®x1, x2‚ü© hf
    simp only [Set.mem_Icc]
    exact ‚ü®candidates_nonneg hf, candidates_le_maxVar hf‚ü©
  ¬∑ refine equicontinuous_of_continuity_modulus (fun t => 2 * maxVar X Y * t) ?_ _ ?_
    ¬∑ have : Tendsto (fun t : ‚Ñù => 2 * (maxVar X Y : ‚Ñù) * t) (ùìù 0) (ùìù (2 * maxVar X Y * 0)) :=
        tendsto_const_nhds.mul tendsto_id
      simpa using this
    ¬∑ rintro x y ‚ü®f, hf‚ü©
      exact (candidates_lipschitz hf).dist_le_mul _ _

/-- We will then choose the candidate minimizing the Hausdorff distance. Except that we are not
in a metric space setting, so we need to define our custom version of Hausdorff distance,
called `HD`, and prove its basic properties. -/
def HD (f : Cb X Y) :=
  max (‚®Ü x, ‚®Ö y, f (inl x, inr y)) (‚®Ü y, ‚®Ö x, f (inl x, inr y))
#align Gromov_Hausdorff.HD GromovHausdorff.HD

/- We will show that `HD` is continuous on `BoundedContinuousFunction`s, to deduce that its
minimum on the compact set `candidatesB` is attained. Since it is defined in terms of
infimum and supremum on `‚Ñù`, which is only conditionally complete, we will need all the time
to check that the defining sets are bounded below or above. This is done in the next few
technical lemmas. -/
theorem HD_below_aux1 {f : Cb X Y} (C : ‚Ñù) {x : X} :
    BddBelow (range fun y : Y => f (inl x, inr y) + C) :=
  let ‚ü®cf, hcf‚ü© := (Real.isBounded_iff_bddBelow_bddAbove.1 f.isBounded_range).1
  ‚ü®cf + C, forall_mem_range.2 fun _ => add_le_add_right ((fun x => hcf (mem_range_self x)) _) _‚ü©
#align Gromov_Hausdorff.HD_below_aux1 GromovHausdorff.HD_below_aux1

private theorem HD_bound_aux1 (f : Cb X Y) (C : ‚Ñù) :
    BddAbove (range fun x : X => ‚®Ö y, f (inl x, inr y) + C) := by
  rcases (Real.isBounded_iff_bddBelow_bddAbove.1 f.isBounded_range).2 with ‚ü®Cf, hCf‚ü©
  refine ‚ü®Cf + C, forall_mem_range.2 fun x => ?_‚ü©
  calc
    ‚®Ö y, f (inl x, inr y) + C ‚â§ f (inl x, inr default) + C := ciInf_le (HD_below_aux1 C) default
    _ ‚â§ Cf + C := add_le_add ((fun x => hCf (mem_range_self x)) _) le_rfl

theorem HD_below_aux2 {f : Cb X Y} (C : ‚Ñù) {y : Y} :
    BddBelow (range fun x : X => f (inl x, inr y) + C) :=
  let ‚ü®cf, hcf‚ü© := (Real.isBounded_iff_bddBelow_bddAbove.1 f.isBounded_range).1
  ‚ü®cf + C, forall_mem_range.2 fun _ => add_le_add_right ((fun x => hcf (mem_range_self x)) _) _‚ü©
#align Gromov_Hausdorff.HD_below_aux2 GromovHausdorff.HD_below_aux2

private theorem HD_bound_aux2 (f : Cb X Y) (C : ‚Ñù) :
    BddAbove (range fun y : Y => ‚®Ö x, f (inl x, inr y) + C) := by
  rcases (Real.isBounded_iff_bddBelow_bddAbove.1 f.isBounded_range).2 with ‚ü®Cf, hCf‚ü©
  refine ‚ü®Cf + C, forall_mem_range.2 fun y => ?_‚ü©
  calc
    ‚®Ö x, f (inl x, inr y) + C ‚â§ f (inl default, inr y) + C := ciInf_le (HD_below_aux2 C) default
    _ ‚â§ Cf + C := add_le_add ((fun x => hCf (mem_range_self x)) _) le_rfl

/-- Explicit bound on `HD (dist)`. This means that when looking for minimizers it will
be sufficient to look for functions with `HD(f)` bounded by this bound. -/
theorem HD_candidatesBDist_le :
    HD (candidatesBDist X Y) ‚â§ diam (univ : Set X) + 1 + diam (univ : Set Y) := by
  refine max_le (ciSup_le fun x => ?_) (ciSup_le fun y => ?_)
  ¬∑ have A : ‚®Ö y, candidatesBDist X Y (inl x, inr y) ‚â§ candidatesBDist X Y (inl x, inr default) :=
      ciInf_le (by simpa using HD_below_aux1 0) default
    have B : dist (inl x) (inr default) ‚â§ diam (univ : Set X) + 1 + diam (univ : Set Y) :=
      calc
        dist (inl x) (inr (default : Y)) = dist x (default : X) + 1 + dist default default := rfl
        _ ‚â§ diam (univ : Set X) + 1 + diam (univ : Set Y) := by
          gcongr <;>
            exact dist_le_diam_of_mem isBounded_of_compactSpace (mem_univ _) (mem_univ _)
    exact le_trans A B
  ¬∑ have A : ‚®Ö x, candidatesBDist X Y (inl x, inr y) ‚â§ candidatesBDist X Y (inl default, inr y) :=
      ciInf_le (by simpa using HD_below_aux2 0) default
    have B : dist (inl default) (inr y) ‚â§ diam (univ : Set X) + 1 + diam (univ : Set Y) :=
      calc
        dist (inl (default : X)) (inr y) = dist default default + 1 + dist default y := rfl
        _ ‚â§ diam (univ : Set X) + 1 + diam (univ : Set Y) := by
          gcongr <;>
            exact dist_le_diam_of_mem isBounded_of_compactSpace (mem_univ _) (mem_univ _)
    exact le_trans A B
#align Gromov_Hausdorff.HD_candidates_b_dist_le GromovHausdorff.HD_candidatesBDist_le

/- To check that `HD` is continuous, we check that it is Lipschitz. As `HD` is a max, we
prove separately inequalities controlling the two terms (relying too heavily on copy-paste...) -/
private theorem HD_lipschitz_aux1 (f g : Cb X Y) :
    (‚®Ü x, ‚®Ö y, f (inl x, inr y)) ‚â§ (‚®Ü x, ‚®Ö y, g (inl x, inr y)) + dist f g := by
  rcases (Real.isBounded_iff_bddBelow_bddAbove.1 g.isBounded_range).1 with ‚ü®cg, hcg‚ü©
  have Hcg : ‚àÄ x, cg ‚â§ g x := fun x => hcg (mem_range_self x)
  rcases (Real.isBounded_iff_bddBelow_bddAbove.1 f.isBounded_range).1 with ‚ü®cf, hcf‚ü©
  have Hcf : ‚àÄ x, cf ‚â§ f x := fun x => hcf (mem_range_self x)
  -- prove the inequality but with `dist f g` inside, by using inequalities comparing
  -- iSup to iSup and iInf to iInf
  have Z : (‚®Ü x, ‚®Ö y, f (inl x, inr y)) ‚â§ ‚®Ü x, ‚®Ö y, g (inl x, inr y) + dist f g :=
    ciSup_mono (HD_bound_aux1 _ (dist f g)) fun x =>
      ciInf_mono ‚ü®cf, forall_mem_range.2 fun i => Hcf _‚ü© fun y => coe_le_coe_add_dist
  -- move the `dist f g` out of the infimum and the supremum, arguing that continuous monotone maps
  -- (here the addition of `dist f g`) preserve infimum and supremum
  have E1 : ‚àÄ x, (‚®Ö y, g (inl x, inr y)) + dist f g = ‚®Ö y, g (inl x, inr y) + dist f g := by
    intro x
    refine Monotone.map_ciInf_of_continuousAt (continuousAt_id.add continuousAt_const) ?_ ?_
    ¬∑ intro x y hx
      simpa
    ¬∑ show BddBelow (range fun y : Y => g (inl x, inr y))
      exact ‚ü®cg, forall_mem_range.2 fun i => Hcg _‚ü©
  have E2 : (‚®Ü x, ‚®Ö y, g (inl x, inr y)) + dist f g = ‚®Ü x, (‚®Ö y, g (inl x, inr y)) + dist f g := by
    refine Monotone.map_ciSup_of_continuousAt (continuousAt_id.add continuousAt_const) ?_ ?_
    ¬∑ intro x y hx
      simpa
    ¬∑ simpa using HD_bound_aux1 _ 0
  -- deduce the result from the above two steps
  simpa [E2, E1, Function.comp]

private theorem HD_lipschitz_aux2 (f g : Cb X Y) :
    (‚®Ü y, ‚®Ö x, f (inl x, inr y)) ‚â§ (‚®Ü y, ‚®Ö x, g (inl x, inr y)) + dist f g := by
  rcases (Real.isBounded_iff_bddBelow_bddAbove.1 g.isBounded_range).1 with ‚ü®cg, hcg‚ü©
  have Hcg : ‚àÄ x, cg ‚â§ g x := fun x => hcg (mem_range_self x)
  rcases (Real.isBounded_iff_bddBelow_bddAbove.1 f.isBounded_range).1 with ‚ü®cf, hcf‚ü©
  have Hcf : ‚àÄ x, cf ‚â§ f x := fun x => hcf (mem_range_self x)
  -- prove the inequality but with `dist f g` inside, by using inequalities comparing
  -- iSup to iSup and iInf to iInf
  have Z : (‚®Ü y, ‚®Ö x, f (inl x, inr y)) ‚â§ ‚®Ü y, ‚®Ö x, g (inl x, inr y) + dist f g :=
    ciSup_mono (HD_bound_aux2 _ (dist f g)) fun y =>
      ciInf_mono ‚ü®cf, forall_mem_range.2 fun i => Hcf _‚ü© fun y => coe_le_coe_add_dist
  -- move the `dist f g` out of the infimum and the supremum, arguing that continuous monotone maps
  -- (here the addition of `dist f g`) preserve infimum and supremum
  have E1 : ‚àÄ y, (‚®Ö x, g (inl x, inr y)) + dist f g = ‚®Ö x, g (inl x, inr y) + dist f g := by
    intro y
    refine Monotone.map_ciInf_of_continuousAt (continuousAt_id.add continuousAt_const) ?_ ?_
    ¬∑ intro x y hx
      simpa
    ¬∑ show BddBelow (range fun x : X => g (inl x, inr y))
      exact ‚ü®cg, forall_mem_range.2 fun i => Hcg _‚ü©
  have E2 : (‚®Ü y, ‚®Ö x, g (inl x, inr y)) + dist f g = ‚®Ü y, (‚®Ö x, g (inl x, inr y)) + dist f g := by
    refine Monotone.map_ciSup_of_continuousAt (continuousAt_id.add continuousAt_const) ?_ ?_
    ¬∑ intro x y hx
      simpa
    ¬∑ simpa using HD_bound_aux2 _ 0
  -- deduce the result from the above two steps
  simpa [E2, E1]

private theorem HD_lipschitz_aux3 (f g : Cb X Y) : HD f ‚â§ HD g + dist f g :=
  max_le (le_trans (HD_lipschitz_aux1 f g) (add_le_add_right (le_max_left _ _) _))
    (le_trans (HD_lipschitz_aux2 f g) (add_le_add_right (le_max_right _ _) _))

/-- Conclude that `HD`, being Lipschitz, is continuous -/
private theorem HD_continuous : Continuous (HD : Cb X Y ‚Üí ‚Ñù) :=
  LipschitzWith.continuous (LipschitzWith.of_le_add HD_lipschitz_aux3)

end Constructions

section Consequences

variable (X : Type u) (Y : Type v) [MetricSpace X] [CompactSpace X] [Nonempty X] [MetricSpace Y]
  [CompactSpace Y] [Nonempty Y]

/- Now that we have proved that the set of candidates is compact, and that `HD` is continuous,
we can finally select a candidate minimizing `HD`. This will be the candidate realizing the
optimal coupling. -/
private theorem exists_minimizer : ‚àÉ f ‚àà candidatesB X Y, ‚àÄ g ‚àà candidatesB X Y, HD f ‚â§ HD g :=
  isCompact_candidatesB.exists_isMinOn candidatesB_nonempty HD_continuous.continuousOn

private def optimalGHDist : Cb X Y :=
  Classical.choose (exists_minimizer X Y)

private theorem optimalGHDist_mem_candidatesB : optimalGHDist X Y ‚àà candidatesB X Y := by
  cases Classical.choose_spec (exists_minimizer X Y)
  assumption

private theorem HD_optimalGHDist_le (g : Cb X Y) (hg : g ‚àà candidatesB X Y) :
    HD (optimalGHDist X Y) ‚â§ HD g :=
  let ‚ü®_, Z2‚ü© := Classical.choose_spec (exists_minimizer X Y)
  Z2 g hg

/-- With the optimal candidate, construct a premetric space structure on `X ‚äï Y`, on which the
predistance is given by the candidate. Then, we will identify points at `0` predistance
to obtain a genuine metric space. -/
def premetricOptimalGHDist : PseudoMetricSpace (X ‚äï Y) where
  dist p q := optimalGHDist X Y (p, q)
  dist_self x := candidates_refl (optimalGHDist_mem_candidatesB X Y)
  dist_comm x y := candidates_symm (optimalGHDist_mem_candidatesB X Y)
  dist_triangle x y z := candidates_triangle (optimalGHDist_mem_candidatesB X Y)
  -- Porting note (#10888): added proof for `edist_dist`
  edist_dist x y := by
    simp only
    congr
    simp only [max, left_eq_sup]
    exact candidates_nonneg (optimalGHDist_mem_candidatesB X Y)
#align Gromov_Hausdorff.premetric_optimal_GH_dist GromovHausdorff.premetricOptimalGHDist

attribute [local instance] premetricOptimalGHDist

/-- A metric space which realizes the optimal coupling between `X` and `Y` -/
-- @[nolint has_nonempty_instance] -- Porting note(#5171): This linter does not exist yet.
def OptimalGHCoupling : Type _ :=
  @SeparationQuotient (X ‚äï Y) (premetricOptimalGHDist X Y).toUniformSpace.toTopologicalSpace
#align Gromov_Hausdorff.optimal_GH_coupling GromovHausdorff.OptimalGHCoupling

instance : MetricSpace (OptimalGHCoupling X Y) := by
  unfold OptimalGHCoupling
  infer_instance

/-- Injection of `X` in the optimal coupling between `X` and `Y` -/
def optimalGHInjl (x : X) : OptimalGHCoupling X Y :=
  Quotient.mk'' (inl x)
#align Gromov_Hausdorff.optimal_GH_injl GromovHausdorff.optimalGHInjl

/-- The injection of `X` in the optimal coupling between `X` and `Y` is an isometry. -/
theorem isometry_optimalGHInjl : Isometry (optimalGHInjl X Y) :=
  Isometry.of_dist_eq fun _ _ => candidates_dist_inl (optimalGHDist_mem_candidatesB X Y) _ _
#align Gromov_Hausdorff.isometry_optimal_GH_injl GromovHausdorff.isometry_optimalGHInjl

/-- Injection of `Y` in the optimal coupling between `X` and `Y` -/
def optimalGHInjr (y : Y) : OptimalGHCoupling X Y :=
  Quotient.mk'' (inr y)
#align Gromov_Hausdorff.optimal_GH_injr GromovHausdorff.optimalGHInjr

/-- The injection of `Y` in the optimal coupling between `X` and `Y` is an isometry. -/
theorem isometry_optimalGHInjr : Isometry (optimalGHInjr X Y) :=
  Isometry.of_dist_eq fun _ _ => candidates_dist_inr (optimalGHDist_mem_candidatesB X Y) _ _
#align Gromov_Hausdorff.isometry_optimal_GH_injr GromovHausdorff.isometry_optimalGHInjr

/-- The optimal coupling between two compact spaces `X` and `Y` is still a compact space -/
instance compactSpace_optimalGHCoupling : CompactSpace (OptimalGHCoupling X Y) := ‚ü®by
  rw [‚Üê range_quotient_mk']
  exact isCompact_range (continuous_sum_dom.2
    ‚ü®(isometry_optimalGHInjl X Y).continuous, (isometry_optimalGHInjr X Y).continuous‚ü©)‚ü©
#align Gromov_Hausdorff.compact_space_optimal_GH_coupling GromovHausdorff.compactSpace_optimalGHCoupling

/-- For any candidate `f`, `HD(f)` is larger than or equal to the Hausdorff distance in the
optimal coupling. This follows from the fact that `HD` of the optimal candidate is exactly
the Hausdorff distance in the optimal coupling, although we only prove here the inequality
we need. -/
theorem hausdorffDist_optimal_le_HD {f} (h : f ‚àà candidatesB X Y) :
    hausdorffDist (range (optimalGHInjl X Y)) (range (optimalGHInjr X Y)) ‚â§ HD f := by
  refine le_trans (le_of_forall_le_of_dense fun r hr => ?_) (HD_optimalGHDist_le X Y f h)
  have A : ‚àÄ x ‚àà range (optimalGHInjl X Y), ‚àÉ y ‚àà range (optimalGHInjr X Y), dist x y ‚â§ r := by
    rintro _ ‚ü®z, rfl‚ü©
    have I1 : (‚®Ü x, ‚®Ö y, optimalGHDist X Y (inl x, inr y)) < r :=
      lt_of_le_of_lt (le_max_left _ _) hr
    have I2 :
        ‚®Ö y, optimalGHDist X Y (inl z, inr y) ‚â§ ‚®Ü x, ‚®Ö y, optimalGHDist X Y (inl x, inr y) :=
      le_csSup (by simpa using HD_bound_aux1 _ 0) (mem_range_self _)
    have I : ‚®Ö y, optimalGHDist X Y (inl z, inr y) < r := lt_of_le_of_lt I2 I1
    rcases exists_lt_of_csInf_lt (range_nonempty _) I with ‚ü®r', ‚ü®z', rfl‚ü©, hr'‚ü©
    exact ‚ü®optimalGHInjr X Y z', mem_range_self _, le_of_lt hr'‚ü©
  refine hausdorffDist_le_of_mem_dist ?_ A ?_
  ¬∑ inhabit X
    rcases A _ (mem_range_self default) with ‚ü®y, -, hy‚ü©
    exact le_trans dist_nonneg hy
  ¬∑ rintro _ ‚ü®z, rfl‚ü©
    have I1 : (‚®Ü y, ‚®Ö x, optimalGHDist X Y (inl x, inr y)) < r :=
      lt_of_le_of_lt (le_max_right _ _) hr
    have I2 :
        ‚®Ö x, optimalGHDist X Y (inl x, inr z) ‚â§ ‚®Ü y, ‚®Ö x, optimalGHDist X Y (inl x, inr y) :=
      le_csSup (by simpa using HD_bound_aux2 _ 0) (mem_range_self _)
    have I : ‚®Ö x, optimalGHDist X Y (inl x, inr z) < r := lt_of_le_of_lt I2 I1
    rcases exists_lt_of_csInf_lt (range_nonempty _) I with ‚ü®r', ‚ü®z', rfl‚ü©, hr'‚ü©
    refine ‚ü®optimalGHInjl X Y z', mem_range_self _, le_of_lt ?_‚ü©
    rwa [dist_comm]
#align Gromov_Hausdorff.Hausdorff_dist_optimal_le_HD GromovHausdorff.hausdorffDist_optimal_le_HD

end Consequences

-- We are done with the construction of the optimal coupling
end GromovHausdorffRealized

end GromovHausdorff
