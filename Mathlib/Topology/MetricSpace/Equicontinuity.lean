/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import Mathlib.Topology.UniformSpace.Equicontinuity
import Mathlib.Topology.MetricSpace.Pseudo.Lemmas

/-!
# Equicontinuity in metric spaces

This file contains various facts about (uniform) equicontinuity in metric spaces. Most
importantly, we prove the usual characterization of equicontinuity of `F` at `xâ‚€` in the case of
(pseudo) metric spaces: `âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ x, dist x xâ‚€ < Î´ â†’ âˆ€ i, dist (F i xâ‚€) (F i x) < Îµ`,
and we prove that functions sharing a common (local or global) continuity modulus are
(locally or uniformly) equicontinuous.

## Main statements

* `Metric.equicontinuousAt_iff`: characterization of equicontinuity for families of functions
  between (pseudo) metric spaces.
* `Metric.equicontinuousAt_of_continuity_modulus`: convenient way to prove equicontinuity at a
  point of a family of functions to a (pseudo) metric space by showing that they share a common
  *local* continuity modulus.
* `Metric.uniformEquicontinuous_of_continuity_modulus`: convenient way to prove uniform
  equicontinuity of a family of functions to a (pseudo) metric space by showing that they share a
  common *global* continuity modulus.

## Tags

equicontinuity, continuity modulus
-/


open Filter Topology Uniformity

variable {Î± Î² Î¹ : Type*} [PseudoMetricSpace Î±]

namespace Metric

/-- Characterization of equicontinuity for families of functions taking values in a (pseudo) metric
space. -/
theorem equicontinuousAt_iff_right {Î¹ : Type*} [TopologicalSpace Î²] {F : Î¹ â†’ Î² â†’ Î±} {xâ‚€ : Î²} :
    EquicontinuousAt F xâ‚€ â†” âˆ€ Îµ > 0, âˆ€á¶  x in ğ“ xâ‚€, âˆ€ i, dist (F i xâ‚€) (F i x) < Îµ :=
  uniformity_basis_dist.equicontinuousAt_iff_right

/-- Characterization of equicontinuity for families of functions between (pseudo) metric spaces. -/
theorem equicontinuousAt_iff {Î¹ : Type*} [PseudoMetricSpace Î²] {F : Î¹ â†’ Î² â†’ Î±} {xâ‚€ : Î²} :
    EquicontinuousAt F xâ‚€ â†” âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ x, dist x xâ‚€ < Î´ â†’ âˆ€ i, dist (F i xâ‚€) (F i x) < Îµ :=
  nhds_basis_ball.equicontinuousAt_iff uniformity_basis_dist

/-- Reformulation of `equicontinuousAt_iff_pair` for families of functions taking values in a
(pseudo) metric space. -/
protected theorem equicontinuousAt_iff_pair {Î¹ : Type*} [TopologicalSpace Î²] {F : Î¹ â†’ Î² â†’ Î±}
    {xâ‚€ : Î²} :
    EquicontinuousAt F xâ‚€ â†”
      âˆ€ Îµ > 0, âˆƒ U âˆˆ ğ“ xâ‚€, âˆ€ x âˆˆ U, âˆ€ x' âˆˆ U, âˆ€ i, dist (F i x) (F i x') < Îµ := by
  rw [equicontinuousAt_iff_pair]
  constructor <;> intro H
  Â· intro Îµ hÎµ
    exact H _ (dist_mem_uniformity hÎµ)
  Â· intro U hU
    rcases mem_uniformity_dist.mp hU with âŸ¨Îµ, hÎµ, hÎµUâŸ©
    refine Exists.imp (fun V => And.imp_right fun h => ?_) (H _ hÎµ)
    exact fun x hx x' hx' i => hÎµU (h _ hx _ hx' i)

/-- Characterization of uniform equicontinuity for families of functions taking values in a
(pseudo) metric space. -/
theorem uniformEquicontinuous_iff_right {Î¹ : Type*} [UniformSpace Î²] {F : Î¹ â†’ Î² â†’ Î±} :
    UniformEquicontinuous F â†” âˆ€ Îµ > 0, âˆ€á¶  xy : Î² Ã— Î² in ğ“¤ Î², âˆ€ i, dist (F i xy.1) (F i xy.2) < Îµ :=
  uniformity_basis_dist.uniformEquicontinuous_iff_right

/-- Characterization of uniform equicontinuity for families of functions between
(pseudo) metric spaces. -/
theorem uniformEquicontinuous_iff {Î¹ : Type*} [PseudoMetricSpace Î²] {F : Î¹ â†’ Î² â†’ Î±} :
    UniformEquicontinuous F â†”
      âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ x y, dist x y < Î´ â†’ âˆ€ i, dist (F i x) (F i y) < Îµ :=
  uniformity_basis_dist.uniformEquicontinuous_iff uniformity_basis_dist

/-- For a family of functions to a (pseudo) metric spaces, a convenient way to prove
equicontinuity at a point is to show that all of the functions share a common *local* continuity
modulus. -/
theorem equicontinuousAt_of_continuity_modulus {Î¹ : Type*} [TopologicalSpace Î²] {xâ‚€ : Î²}
    (b : Î² â†’ â„) (b_lim : Tendsto b (ğ“ xâ‚€) (ğ“ 0)) (F : Î¹ â†’ Î² â†’ Î±)
    (H : âˆ€á¶  x in ğ“ xâ‚€, âˆ€ i, dist (F i xâ‚€) (F i x) â‰¤ b x) : EquicontinuousAt F xâ‚€ := by
  rw [Metric.equicontinuousAt_iff_right]
  intro Îµ Îµ0
  filter_upwards [b_lim (Iio_mem_nhds Îµ0), H] using fun x hxâ‚ hxâ‚‚ i => (hxâ‚‚ i).trans_lt hxâ‚

/-- For a family of functions between (pseudo) metric spaces, a convenient way to prove
uniform equicontinuity is to show that all of the functions share a common *global* continuity
modulus. -/
theorem uniformEquicontinuous_of_continuity_modulus {Î¹ : Type*} [PseudoMetricSpace Î²] (b : â„ â†’ â„)
    (b_lim : Tendsto b (ğ“ 0) (ğ“ 0)) (F : Î¹ â†’ Î² â†’ Î±)
    (H : âˆ€ (x y : Î²) (i), dist (F i x) (F i y) â‰¤ b (dist x y)) : UniformEquicontinuous F := by
  rw [Metric.uniformEquicontinuous_iff]
  intro Îµ Îµ0
  rcases tendsto_nhds_nhds.1 b_lim Îµ Îµ0 with âŸ¨Î´, Î´0, hÎ´âŸ©
  refine âŸ¨Î´, Î´0, fun x y hxy i => ?_âŸ©
  calc
    dist (F i x) (F i y) â‰¤ b (dist x y) := H x y i
    _ â‰¤ |b (dist x y)| := le_abs_self _
    _ = dist (b (dist x y)) 0 := by simp [Real.dist_eq]
    _ < Îµ := hÎ´ (by simpa only [Real.dist_eq, tsub_zero, abs_dist] using hxy)

/-- For a family of functions between (pseudo) metric spaces, a convenient way to prove
equicontinuity is to show that all of the functions share a common *global* continuity modulus. -/
theorem equicontinuous_of_continuity_modulus {Î¹ : Type*} [PseudoMetricSpace Î²] (b : â„ â†’ â„)
    (b_lim : Tendsto b (ğ“ 0) (ğ“ 0)) (F : Î¹ â†’ Î² â†’ Î±)
    (H : âˆ€ (x y : Î²) (i), dist (F i x) (F i y) â‰¤ b (dist x y)) : Equicontinuous F :=
  (uniformEquicontinuous_of_continuity_modulus b b_lim F H).equicontinuous

end Metric
