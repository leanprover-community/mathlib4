/-
Copyright (c) 2025 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Order.CompleteLattice.Group
import Mathlib.Topology.ContinuousMap.Bounded.Basic
import Mathlib.Topology.ContinuousMap.Compact
import Mathlib.Topology.MetricSpace.Lipschitz
import Mathlib.Topology.UniformSpace.UniformConvergenceTopology

/-! # Metric structure on `Î± â†’áµ¤ Î²` and `Î± â†’áµ¤[ğ”–] Î²` for finite `ğ”–`

When `Î²` is a (pseudo, extended) metric space it is a uniform space, and therefore we may
consider the type `Î± â†’áµ¤ Î²` of functions equipped with the topology of uniform convergence. The
natural (pseudo, extended) metric on this space is given by `fun f g â†¦ â¨† x, edist (f x) (g x)`,
and this induces the existing uniformity. Unless `Î²` is a bounded space, this will not be a (pseudo)
metric space (except in the trivial case where `Î±` is empty).

When `ğ”– : Set (Set Î±)` is a collection of subsets, we may equip the space of functions with the
(pseudo, extended) metric `fun f g â†¦ â¨† x âˆˆ â‹ƒâ‚€ ğ”–, edist (f x) (g x)`. *However*, this only induces
the pre-existing uniformity on `Î± â†’áµ¤[ğ”–] Î²` if `ğ”–` is finite, and hence we only have an instance in
that case. Nevertheless, this still covers the most important case, such as when `ğ”–` is a singleton.

Furthermore, we note that this is essentially a mathematical obstruction, not a technical one:
indeed, the uniformity of `Î± â†’áµ¤[ğ”–] Î²` is countably generated only when there is a sequence
`t : â„• â†’ Finset (Set Î±)` such that, for each `n`, `t n âŠ† ğ”–`, `fun n â†¦ Finset.sup (t n)` is monotone
and for every `s âˆˆ ğ”–`, there is some `n` such that `s âŠ† Finset.sup (t n)` (see
`UniformOnFun.isCountablyGenerated_uniformity`). So, while the `ğ”–` for which `Î± â†’áµ¤[ğ”–] Î²` is
metrizable include some non-finite `ğ”–`, there are some `ğ”–` which are not metrizable, and moreover,
it is only when `ğ”–` is finite that `â¨† x âˆˆ â‹ƒâ‚€ ğ”–, edist (f x) (g x)` is a metric which induces the
uniformity.

There are a few advantages of equipping this space with this metric structure.

1. A function `f : X â†’ Î± â†’áµ¤ Î²` is Lipschitz in this metric if and only if for every `a : Î±` it is
  Lipschitz in the first variable with the same Lipschitz constant.
2. It provides a natural setting in which one can talk about the metrics on `Î± â†’áµ‡ Î²` or, when
  `Î±` is compact, `C(Î±, Î²)`, relative to their underlying bare functions.
-/

variable {Î± Î² Î³ : Type*} [PseudoEMetricSpace Î³]
open scoped UniformConvergence NNReal ENNReal
open Filter Topology

namespace UniformFun

section EMetric

variable [PseudoEMetricSpace Î²]

/-- The natural `EMetric` structure on `Î± â†’áµ¤ Î²` given by `edist f g = â¨† x, edist (f x) (g x)`. -/
noncomputable instance : PseudoEMetricSpace (Î± â†’áµ¤ Î²) where
  edist f g := â¨† x, edist (f x) (g x)
  edist_self := by simp
  edist_comm := by simp [edist_comm]
  edist_triangle fâ‚ fâ‚‚ fâ‚ƒ := calc
    â¨† x, edist (fâ‚ x) (fâ‚ƒ x) â‰¤ â¨† x, edist (fâ‚ x) (fâ‚‚ x) + edist (fâ‚‚ x) (fâ‚ƒ x) :=
      iSup_mono fun _ â†¦ edist_triangle _ _ _
    _ â‰¤ (â¨† x, edist (fâ‚ x) (fâ‚‚ x)) + (â¨† x, edist (fâ‚‚ x) (fâ‚ƒ x)) := iSup_add_le _ _
  toUniformSpace := inferInstance
  uniformity_edist := by
    rw [UniformFun.hasBasis_uniformity_of_basis Î± Î² uniformity_basis_edist |>.eq_biInf]
    simp only [Function.comp_apply, UniformFun.gen, Set.mem_setOf_eq]
    refine le_antisymm ?_ <| iInfâ‚‚_mono ?_
    Â· refine iInfâ‚‚_mono' fun Îµ hÎµ â†¦ ?_
      obtain âŸ¨Î´, hÎ´, hÎ´ÎµâŸ© := exists_between hÎµ
      exact âŸ¨Î´, hÎ´, by simpa [iSup_lt_iff] using fun f g h â†¦ âŸ¨Î´, hÎ´Îµ, fun x â†¦ (h x).leâŸ©âŸ©
    Â· simpa using fun Îµ hÎµ f g h x â†¦ (le_iSup _ x).trans_lt h

lemma edist_def (f g : Î± â†’áµ¤ Î²) :
    edist f g = â¨† x, edist (toFun f x) (toFun g x) :=
  rfl

noncomputable instance {Î² : Type*} [EMetricSpace Î²] : EMetricSpace (Î± â†’áµ¤ Î²) where
  eq_of_edist_eq_zero {f g} h := funext fun x â†¦ eq_of_edist_eq_zero <| le_antisymm
    ((edist_def f g â–¸ h) â–¸ le_iSup (fun y â†¦ edist (f y) (g y)) x) (zero_le _)

lemma lipschitzWith_iff {f : Î³ â†’ Î± â†’áµ¤ Î²} {K : â„â‰¥0} :
    LipschitzWith K f â†” âˆ€ c, LipschitzWith K (fun x â†¦ toFun (f x) c) := by
  simp [LipschitzWith, edist_def, forall_comm (Î± := Î±), toFun, ofFun]

lemma lipschitzWith_ofFun_iff {f : Î³ â†’ Î± â†’ Î²} {K : â„â‰¥0} :
    LipschitzWith K (fun x â†¦ ofFun (f x)) â†” âˆ€ c, LipschitzWith K (f Â· c) :=
  lipschitzWith_iff

/-- If `f : Î± â†’ Î³ â†’ Î²` is a family of a functions, all of which are Lipschitz with the
same constant, then the family is uniformly equicontinuous. -/
lemma _root_.LipschitzWith.uniformEquicontinuous (f : Î± â†’ Î³ â†’ Î²) (K : â„â‰¥0)
    (h : âˆ€ c, LipschitzWith K (f c)) : UniformEquicontinuous f := by
  rw [uniformEquicontinuous_iff_uniformContinuous]
  rw [â† lipschitzWith_ofFun_iff] at h
  exact h.uniformContinuous

lemma lipschitzOnWith_iff {f : Î³ â†’ Î± â†’áµ¤ Î²} {K : â„â‰¥0} {s : Set Î³} :
    LipschitzOnWith K f s â†” âˆ€ c, LipschitzOnWith K (fun x â†¦ toFun (f x) c) s := by
  simp [lipschitzOnWith_iff_restrict, lipschitzWith_iff]
  rfl

lemma lipschitzWith_eval (x : Î±) :
    LipschitzWith 1 (fun f : Î± â†’áµ¤ Î² â†¦ f x) := by
  intro f g
  simpa [edist_def] using le_iSup (fun y â†¦ edist (toFun f y) (toFun g y)) x

end EMetric

section Metric

variable [PseudoMetricSpace Î²]

noncomputable instance [BoundedSpace Î²] : PseudoMetricSpace (Î± â†’áµ¤ Î²) :=
  PseudoEMetricSpace.toPseudoMetricSpaceOfDist
    (fun f g â†¦ â¨† x, dist (toFun f x) (toFun g x))
    (fun _ _ â†¦ by
      have := BoundedSpace.bounded_univ (Î± := Î²) |>.ediam_ne_top.lt_top
      refine (iSup_le fun x â†¦ EMetric.edist_le_diam_of_mem ?_ ?_).trans_lt this |>.ne
      all_goals trivial)
    (fun _ _ â†¦ by simp [edist_def, ENNReal.toReal_iSup (fun _ â†¦ edist_ne_top _ _), dist_edist])

noncomputable instance [BoundedSpace Î²] : BoundedSpace (Î± â†’áµ¤ Î²) where
  bounded_univ := by
    rw [Metric.isBounded_iff_ediam_ne_top, â† lt_top_iff_ne_top]
    refine lt_of_le_of_lt ?_ <| BoundedSpace.bounded_univ (Î± := Î²) |>.ediam_ne_top.lt_top
    simp only [EMetric.diam_le_iff, Set.mem_univ, edist_def, iSup_le_iff, forall_const]
    exact fun f g x â†¦ EMetric.edist_le_diam_of_mem (by trivial) (by trivial)

noncomputable instance {Î² : Type*} [MetricSpace Î²] [BoundedSpace Î²] : MetricSpace (Î± â†’áµ¤ Î²) where
  eq_of_dist_eq_zero {f g} h := by
    rw [dist_edist, ENNReal.toReal_eq_zero_iff] at h
    exact eq_of_edist_eq_zero <| h.resolve_right <| edist_ne_top f g

open BoundedContinuousFunction in
lemma isometry_ofFun_boundedContinuousFunction [TopologicalSpace Î±] :
    Isometry (ofFun âˆ˜ DFunLike.coe : (Î± â†’áµ‡ Î²) â†’ Î± â†’áµ¤ Î²) := by
  simp [Isometry, edist_def, edist_eq_iSup]

lemma isometry_ofFun_continuousMap [TopologicalSpace Î±] [CompactSpace Î±] :
    Isometry (ofFun âˆ˜ DFunLike.coe : C(Î±, Î²) â†’ Î± â†’áµ¤ Î²) :=
  isometry_ofFun_boundedContinuousFunction.comp <|
    ContinuousMap.isometryEquivBoundedOfCompact Î± Î² |>.isometry

lemma edist_continuousMapMk [TopologicalSpace Î±] [CompactSpace Î±]
    {f g : Î± â†’áµ¤ Î²} (hf : Continuous (toFun f)) (hg : Continuous (toFun g)) :
    edist (âŸ¨_, hfâŸ© : C(Î±, Î²)) âŸ¨_, hgâŸ© = edist f g := by
  simp [â† isometry_ofFun_continuousMap.edist_eq]

end Metric

end UniformFun

namespace UniformOnFun

variable {ğ”– ğ”— : Set (Set Î±)}

section EMetric

variable [PseudoEMetricSpace Î²]

/-- Let `f : Î³ â†’ Î± â†’áµ¤[ğ”–] Î²`. If for every `s âˆˆ ğ”–` and for every `c âˆˆ s`, the fucntion
`fun x â†¦ f x c` is Lipschitz (with Lipschitz constant depending on `s`), then `f` is continuous. -/
lemma continuous_of_forall_lipschitzWith {f : Î³ â†’ Î± â†’áµ¤[ğ”–] Î²} (K : Set Î± â†’ â„â‰¥0)
    (h : âˆ€ s âˆˆ ğ”–, âˆ€ c âˆˆ s, LipschitzWith (K s) (fun x â†¦ toFun ğ”– (f x) c)) :
    Continuous f := by
  rw [UniformOnFun.continuous_rng_iff]
  revert h
  congr! with h s hs
  refine LipschitzWith.continuous (K := K s) ?_
  rw [UniformFun.lipschitzWith_iff]
  rintro âŸ¨y, hyâŸ©
  exact h s hs y hy

variable [Finite ğ”–]

noncomputable instance : PseudoEMetricSpace (Î± â†’áµ¤[ğ”–] Î²) where
  edist f g := â¨† x âˆˆ â‹ƒâ‚€ ğ”–, edist (f x) (g x)
  edist_self := by simp
  edist_comm := by simp [edist_comm]
  edist_triangle fâ‚ fâ‚‚ fâ‚ƒ := calc
    â¨† x âˆˆ â‹ƒâ‚€ ğ”–, edist (fâ‚ x) (fâ‚ƒ x) â‰¤ â¨† x âˆˆ â‹ƒâ‚€ ğ”–, edist (fâ‚ x) (fâ‚‚ x) + edist (fâ‚‚ x) (fâ‚ƒ x) :=
      iSupâ‚‚_mono fun _ _ â†¦ edist_triangle _ _ _
    _ â‰¤ (â¨† x âˆˆ â‹ƒâ‚€ ğ”–, edist (fâ‚ x) (fâ‚‚ x)) + (â¨† x âˆˆ â‹ƒâ‚€ ğ”–, edist (fâ‚‚ x) (fâ‚ƒ x)) := iSupâ‚‚_add_le _ _
  toUniformSpace := inferInstance
  uniformity_edist := by
    trans â¨… Îµ > 0, Filter.principal {p | â¨† x âˆˆ â‹ƒâ‚€ ğ”–, edist (p.1 x) (p.2 x) â‰¤ Îµ}
    Â· rw [UniformOnFun.uniformity_eq_of_basis Î² ğ”– uniformity_basis_edist_le]
      simp [UniformOnFun.gen, iSup_le_iff, toFun, ofFun, iInfâ‚‚_comm (Î¹â‚‚ := â„â‰¥0âˆ),
        iInf_principal_finite â€¹_â€º, Set.iInter_setOf, forall_comm (Î± := Î±)]
    refine le_antisymm ?_ (iInfâ‚‚_mono ?_)
    Â· refine iInfâ‚‚_mono' fun Îµ hÎµ â†¦ ?_
      obtain âŸ¨Î´, hÎ´, hÎ´ÎµâŸ© := exists_between hÎµ
      exact âŸ¨Î´, hÎ´, by simpa [iSup_lt_iff] using fun f g h â†¦ âŸ¨Î´, hÎ´Îµ, hâŸ©âŸ©
    Â· simp only [gt_iff_lt, iSup_le_iff, Filter.le_principal_iff, Filter.mem_principal,
        Set.setOf_subset_setOf, Prod.forall]
      exact fun Îµ hÎµ f g h x hx â†¦ (le_iSupâ‚‚ (f := fun x _ â†¦ edist (f x) (g x)) x hx).trans h.le

lemma edist_def (f g : Î± â†’áµ¤[ğ”–] Î²) :
    edist f g = â¨† x âˆˆ â‹ƒâ‚€ ğ”–, edist (toFun ğ”– f x) (toFun ğ”– g x) :=
  rfl

lemma edist_def' (f g : Î± â†’áµ¤[ğ”–] Î²) :
    edist f g = â¨† s âˆˆ ğ”–, â¨† x âˆˆ s, edist (toFun ğ”– f x) (toFun ğ”– g x) := by
  simp [edist_def, iSup_and, iSup_comm (Î¹ := Î±)]

lemma lipschitzWith_iff {f : Î³ â†’ Î± â†’áµ¤[ğ”–] Î²} {K : â„â‰¥0} :
    LipschitzWith K f â†” âˆ€ c âˆˆ â‹ƒâ‚€ ğ”–, LipschitzWith K (fun x â†¦ toFun ğ”– (f x) c) := by
  simp [LipschitzWith, edist_def, toFun, ofFun]
  tauto

lemma lipschitzOnWith_iff {f : Î³ â†’ Î± â†’áµ¤[ğ”–] Î²} {K : â„â‰¥0} {s : Set Î³} :
    LipschitzOnWith K f s â†” âˆ€ c âˆˆ â‹ƒâ‚€ ğ”–, LipschitzOnWith K (fun x â†¦ toFun ğ”– (f x) c) s := by
  simp [lipschitzOnWith_iff_restrict, lipschitzWith_iff]
  rfl

lemma lipschitzWith_eval (x : Î±) (hx : x âˆˆ â‹ƒâ‚€ ğ”–) :
    LipschitzWith 1 (fun f : Î± â†’áµ¤[ğ”–] Î² â†¦ toFun ğ”– f x) := by
  intro f g
  simpa only [ENNReal.coe_one, one_mul] using
    le_iSupâ‚‚ (f := fun y _ â†¦ edist (toFun ğ”– f y) (toFun ğ”– g y)) x hx

lemma lipschitzWith_one_ofFun_toFun :
    LipschitzWith 1 (ofFun ğ”– âˆ˜ UniformFun.toFun : (Î± â†’áµ¤ Î²) â†’ (Î± â†’áµ¤[ğ”–] Î²)) :=
  lipschitzWith_iff.mpr fun _ _ â†¦ UniformFun.lipschitzWith_eval _

lemma lipschitzWith_one_ofFun_toFun' [Finite ğ”—] (h : â‹ƒâ‚€ ğ”– âŠ† â‹ƒâ‚€ ğ”—) :
    LipschitzWith 1 (ofFun ğ”– âˆ˜ toFun ğ”— : (Î± â†’áµ¤[ğ”—] Î²) â†’ (Î± â†’áµ¤[ğ”–] Î²)) :=
  lipschitzWith_iff.mpr fun x hx â†¦ lipschitzWith_eval x (h hx)

lemma lipschitzWith_restrict (s : Set Î±) (hs : s âˆˆ ğ”–)  :
    LipschitzWith 1 (UniformFun.ofFun âˆ˜ s.restrict âˆ˜ toFun ğ”– : (Î± â†’áµ¤[ğ”–] Î²) â†’ (s â†’áµ¤ Î²)) :=
  UniformFun.lipschitzWith_iff.mpr fun x â†¦ lipschitzWith_eval _ âŸ¨s, hs, x.2âŸ©

lemma isometry_restrict (s : Set Î±) :
    Isometry (UniformFun.ofFun âˆ˜ s.restrict âˆ˜ toFun {s} : (Î± â†’áµ¤[{s}] Î²) â†’ (s â†’áµ¤ Î²)) := by
  simp [Isometry, edist_def, UniformFun.edist_def, iSup_subtype]

end EMetric

section Metric

variable [Finite ğ”–] [PseudoMetricSpace Î²]

noncomputable instance [BoundedSpace Î²] : PseudoMetricSpace (Î± â†’áµ¤[ğ”–] Î²) :=
  PseudoEMetricSpace.toPseudoMetricSpaceOfDist
    (fun f g â†¦ â¨† x âˆˆ â‹ƒâ‚€ ğ”–, dist (toFun ğ”– f x) (toFun ğ”– g x))
    (fun _ _ â†¦ by
      have := BoundedSpace.bounded_univ (Î± := Î²) |>.ediam_ne_top.lt_top
      refine (iSupâ‚‚_le fun x _ â†¦ EMetric.edist_le_diam_of_mem ?_ ?_).trans_lt this |>.ne
      all_goals trivial)
    (fun _ _ â†¦ by
      simp only [dist_edist, edist_def, iSup_sigma']
      rw [ENNReal.toReal_iSup]
      Â· congr!
        rw [ENNReal.toReal_iSup]
        exact (fun _ â†¦ edist_ne_top _ _)
      Â· have := BoundedSpace.bounded_univ (Î± := Î²) |>.ediam_ne_top.lt_top
        refine fun x â†¦ lt_of_le_of_lt (iSup_le fun hx â†¦ ?_) this |>.ne
        exact EMetric.edist_le_diam_of_mem (by trivial) (by trivial))

noncomputable instance [BoundedSpace Î²] : BoundedSpace (Î± â†’áµ¤[ğ”–] Î²) where
  bounded_univ := by
    convert lipschitzWith_one_ofFun_toFun (ğ”– := ğ”–) (Î² := Î²) |>.isBounded_image (.all Set.univ)
    ext f
    simp only [Set.mem_univ, Function.comp_apply, Set.image_univ, Set.mem_range, true_iff]
    exact âŸ¨UniformFun.ofFun (toFun ğ”– f), by simpâŸ©

lemma edist_continuousRestrict [TopologicalSpace Î±] {f g : Î± â†’áµ¤[ğ”–] Î²}
    [CompactSpace (â‹ƒâ‚€ ğ”–)] (hf : ContinuousOn (toFun ğ”– f) (â‹ƒâ‚€ ğ”–))
    (hg : ContinuousOn (toFun ğ”– g) (â‹ƒâ‚€ ğ”–)) :
    edist (âŸ¨_, hf.restrictâŸ© : C(â‹ƒâ‚€ ğ”–, Î²)) âŸ¨_, hg.restrictâŸ© = edist f g := by
  simp [ContinuousMap.edist_eq_iSup, iSup_subtype, edist_def]

lemma edist_continuousRestrict_of_singleton [TopologicalSpace Î±] {s : Set Î±}
    {f g : Î± â†’áµ¤[{s}] Î²} [CompactSpace s] (hf : ContinuousOn (toFun {s} f) s)
    (hg : ContinuousOn (toFun {s} g) s) :
    edist (âŸ¨_, hf.restrictâŸ© : C(s, Î²)) âŸ¨_, hg.restrictâŸ© = edist f g := by
  simp [ContinuousMap.edist_eq_iSup, iSup_subtype, edist_def]

end Metric

end UniformOnFun
