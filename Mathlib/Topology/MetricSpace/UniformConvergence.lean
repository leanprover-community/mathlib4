/-
Copyright (c) 2025 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Topology.ContinuousMap.Bounded.Basic
import Mathlib.Topology.ContinuousMap.Compact
import Mathlib.Topology.MetricSpace.Lipschitz
import Mathlib.Topology.UniformSpace.UniformConvergenceTopology


/-! # Metric structure on `Î± â†’áµ¤ Î²` -/

section iSupMul

variable {Î± : Type*} {Î¹ : Sort*} {Îº : Î¹ â†’ Sort*}
  [CompleteLattice Î±] [Mul Î±] [MulLeftMono Î±] [MulRightMono Î±]

@[to_additive]
lemma iSup_mul_le (u v : Î¹ â†’ Î±) :
    â¨† i, u i * v i â‰¤ (â¨† i, u i) * â¨† i, v i :=
  iSup_le fun _ â†¦ mul_le_mul' (le_iSup _ _) (le_iSup _ _)

@[to_additive]
lemma le_iInf_mul (u v : Î¹ â†’ Î±) :
    (â¨… i, u i) * â¨… i, v i â‰¤ â¨… i, u i * v i :=
  iSup_mul_le (Î± := Î±áµ’áµˆ) _ _

@[to_additive]
lemma iSupâ‚‚_mul_le (u v : (i : Î¹) â†’ Îº i â†’ Î±) :
    â¨† (i) (j), u i j * v i j â‰¤ (â¨† (i) (j), u i j) * â¨† (i) (j), v i j := by
  refine le_trans ?_ (iSup_mul_le _ _)
  gcongr
  exact iSup_mul_le _ _

@[to_additive]
lemma le_iInfâ‚‚_mul (u v : (i : Î¹) â†’ Îº i â†’ Î±) :
    (â¨… (i) (j), u i j) * â¨… (i) (j), v i j â‰¤ â¨… (i) (j), u i j * v i j :=
  iSupâ‚‚_mul_le (Î± := Î±áµ’áµˆ) _ _

end iSupMul

theorem BoundedContinuousFunction.edist_eq_iSup {Î± Î² : Type*} [TopologicalSpace Î±]
    [PseudoMetricSpace Î²] {f g : BoundedContinuousFunction Î± Î²} :
    edist f g = â¨† (x : Î±), edist (f x) (g x) := by
  simp_rw [edist_nndist, nndist_eq_iSup]
  refine ENNReal.coe_iSup âŸ¨nndist f g, ?_âŸ©
  rintro - âŸ¨x, hx, rflâŸ©
  exact nndist_coe_le_nndist x

variable {Î± Î² Î³ : Type*}

open scoped UniformConvergence NNReal
open Filter Topology

namespace UniformFun

noncomputable instance [PseudoEMetricSpace Î²] : PseudoEMetricSpace (Î± â†’áµ¤ Î²) where
  edist f g := â¨† x, edist (f x) (g x)
  edist_self := by simp
  edist_comm := by simp [edist_comm]
  edist_triangle fâ‚ fâ‚‚ fâ‚ƒ := calc
    â¨† x, edist (fâ‚ x) (fâ‚ƒ x) â‰¤ â¨† x, edist (fâ‚ x) (fâ‚‚ x) + edist (fâ‚‚ x) (fâ‚ƒ x) :=
      iSup_mono fun _ â†¦ edist_triangle _ _ _
    _ â‰¤ (â¨† x, edist (fâ‚ x) (fâ‚‚ x)) + (â¨† x, edist (fâ‚‚ x) (fâ‚ƒ x)) := iSup_add_le _ _
  toUniformSpace := inferInstance
  uniformity_edist := by
    rw [UniformFun.hasBasis_uniformity_of_basis Î± Î² uniformity_basis_edist |>.eq_biInf]
    simp only [Function.comp_apply, UniformFun.gen, Set.mem_setOf_eq]
    refine le_antisymm ?_ <| iInfâ‚‚_mono ?_
    Â· refine iInfâ‚‚_mono' fun Îµ hÎµ â†¦ ?_
      obtain âŸ¨Î´, hÎ´, hÎ´ÎµâŸ© := exists_between hÎµ
      exact âŸ¨Î´, hÎ´, by simpa [iSup_lt_iff] using fun f g h â†¦ âŸ¨Î´, hÎ´Îµ, fun x â†¦ (h x).leâŸ©âŸ©
    Â· simpa using fun Îµ hÎµ f g h x â†¦ (le_iSup _ x).trans_lt h

lemma edist_def [PseudoEMetricSpace Î²] (f g : Î± â†’áµ¤ Î²) :
    edist f g = â¨† x, edist (toFun f x) (toFun g x) :=
  rfl

noncomputable instance [EMetricSpace Î²] : EMetricSpace (Î± â†’áµ¤ Î²) where
  eq_of_edist_eq_zero {f g} h := funext fun x â†¦ eq_of_edist_eq_zero <| le_antisymm
    ((edist_def f g â–¸ h) â–¸ le_iSup (fun y â†¦ edist (f y) (g y)) x) (zero_le _)

noncomputable instance [PseudoMetricSpace Î²] [BoundedSpace Î²] :
    PseudoMetricSpace (Î± â†’áµ¤ Î²) :=
  PseudoEMetricSpace.toPseudoMetricSpaceOfDist
    (fun f g â†¦ â¨† x, dist (toFun f x) (toFun g x))
    (fun _ _ â†¦ by
      have := BoundedSpace.bounded_univ (Î± := Î²) |>.ediam_ne_top.lt_top
      refine (iSup_le fun x â†¦ EMetric.edist_le_diam_of_mem ?_ ?_).trans_lt this |>.ne
      all_goals trivial)
    (fun _ _ â†¦ by simp [edist_def, ENNReal.toReal_iSup (fun _ â†¦ edist_ne_top _ _), dist_edist])

noncomputable instance [MetricSpace Î²] [BoundedSpace Î²] :
    MetricSpace (Î± â†’áµ¤ Î²) where
  eq_of_dist_eq_zero {f g} h := by
    rw [dist_edist, ENNReal.toReal_eq_zero_iff] at h
    exact eq_of_edist_eq_zero <| h.resolve_right <| edist_ne_top f g

lemma lipschitzWith_iff [PseudoEMetricSpace Î²] [PseudoEMetricSpace Î³] {f : Î³ â†’ Î± â†’áµ¤ Î²} {K : â„â‰¥0} :
    LipschitzWith K f â†” âˆ€ c, LipschitzWith K (fun x â†¦ toFun (f x) c) := by
  simp [LipschitzWith, edist_def, forall_comm (Î± := Î±), toFun, ofFun]

open BoundedContinuousFunction in
@[simp]
lemma edist_ofFun_boundedContinuousFunction [PseudoMetricSpace Î²] [TopologicalSpace Î±]
    {f g : Î± â†’áµ‡ Î²} :
    edist (ofFun f) (ofFun g) = edist f g := by
  simp [edist_def, edist_eq_iSup]

@[simp]
lemma edist_ofFun_continuousMap [PseudoMetricSpace Î²] [TopologicalSpace Î±] [CompactSpace Î±]
    {f g : C(Î±, Î²)} :
    edist (ofFun f) (ofFun g) = edist f g := by
  refine Eq.trans ?_ <| (ContinuousMap.isometryEquivBoundedOfCompact Î± Î²).edist_eq f g
  exact edist_ofFun_boundedContinuousFunction (f := ContinuousMap.equivBoundedOfCompact Î± Î² f)
    (g := ContinuousMap.equivBoundedOfCompact Î± Î² g)

lemma edist_continuousMapMk [PseudoMetricSpace Î²] [TopologicalSpace Î±] [CompactSpace Î±]
    {f g : Î± â†’áµ¤ Î²} (hf : Continuous (toFun f)) (hg : Continuous (toFun g)) :
    edist (âŸ¨_, hfâŸ© : C(Î±, Î²)) âŸ¨_, hgâŸ© = edist f g := by
  simp [â† edist_ofFun_continuousMap]

end UniformFun

namespace UniformOnFun

variable {ð”– ð”— : Set (Set Î±)}

lemma uniformContinuous_ofFun_toFun [UniformSpace Î²] (h : âˆ€ s âˆˆ ð”–, âˆƒ T âŠ† ð”—, T.Finite âˆ§ s âŠ† â‹ƒâ‚€ T) :
    UniformContinuous (ofFun ð”— âˆ˜ toFun ð”– : (Î± â†’áµ¤[ð”—] Î²) â†’ Î± â†’áµ¤[ð”–] Î²) := by
  simp only [UniformContinuous, UniformOnFun.uniformity_eq, iInfâ‚‚_comm (Î¹â‚‚ := Set (Î² Ã— Î²))]
  refine tendsto_iInf_iInf fun V â†¦ tendsto_iInf_iInf fun hV â†¦ ?_
  simp only [tendsto_iInf, tendsto_principal]
  intro s hs
  rw [Filter.Eventually]
  simp only [mem_biInf_principal]
  obtain âŸ¨T, hTð”—, hT, hsTâŸ© := h s hs
  use T, hT, hTð”—
  intro f hf
  simp only [UniformOnFun.gen, Set.mem_iInter, Set.mem_setOf_eq, Function.comp_apply] at hf âŠ¢
  intro x hx
  obtain âŸ¨t, ht, hxtâŸ© := Set.mem_sUnion.mp <| hsT hx
  exact hf t ht x hxt

/-- Let `f : Î³ â†’ Î± â†’áµ¤[ð”–] Î²`. If for every `s âˆˆ ð”–` and for every `c âˆˆ s`, the fucntion
`fun x â†¦ f x c` is Lipschitz (with Lipschitz constant depending on `s`), then `f` is continuous. -/
lemma continuous_of_forall_lipschitzWith [PseudoEMetricSpace Î²] [PseudoEMetricSpace Î³]
    {f : Î³ â†’ Î± â†’áµ¤[ð”–] Î²} (K : Set Î± â†’ â„â‰¥0)
    (h : âˆ€ s âˆˆ ð”–, âˆ€ c âˆˆ s, LipschitzWith (K s) (fun x â†¦ toFun ð”– (f x) c)) :
    Continuous f := by
  rw [UniformOnFun.continuous_rng_iff]
  revert h
  congr! with h s hs
  refine LipschitzWith.continuous (K := K s) ?_
  rw [UniformFun.lipschitzWith_iff]
  rintro âŸ¨y, hyâŸ©
  exact h s hs y hy

noncomputable instance {s : Set Î±} [PseudoEMetricSpace Î²] : PseudoEMetricSpace (Î± â†’áµ¤[{s}] Î²) where
  edist f g := â¨† x âˆˆ s, edist (f x) (g x)
  edist_self := by simp
  edist_comm := by simp [edist_comm]
  edist_triangle fâ‚ fâ‚‚ fâ‚ƒ := calc
    â¨† x âˆˆ s, edist (fâ‚ x) (fâ‚ƒ x) â‰¤ â¨† x âˆˆ s, edist (fâ‚ x) (fâ‚‚ x) + edist (fâ‚‚ x) (fâ‚ƒ x) :=
      iSupâ‚‚_mono fun _ _ â†¦ edist_triangle _ _ _
    _ â‰¤ (â¨† x âˆˆ s, edist (fâ‚ x) (fâ‚‚ x)) + (â¨† x âˆˆ s, edist (fâ‚‚ x) (fâ‚ƒ x)) := iSupâ‚‚_add_le _ _
  toUniformSpace := inferInstance
  uniformity_edist := by
    trans â¨… Îµ > 0, Filter.principal {p | â¨† x âˆˆ s, edist (p.1 x) (p.2 x) â‰¤ Îµ}
    Â· rw [UniformOnFun.uniformity_eq_of_basis Î² {s} uniformity_basis_edist_le]
      simp [UniformOnFun.gen, iSup_le_iff, toFun, ofFun]
    refine le_antisymm ?_ (iInfâ‚‚_mono ?_)
    Â· refine iInfâ‚‚_mono' fun Îµ hÎµ â†¦ ?_
      obtain âŸ¨Î´, hÎ´, hÎ´ÎµâŸ© := exists_between hÎµ
      exact âŸ¨Î´, hÎ´, by simpa [iSup_lt_iff] using fun f g h â†¦ âŸ¨Î´, hÎ´Îµ, hâŸ©âŸ©
    Â· simp only [gt_iff_lt, iSup_le_iff, Filter.le_principal_iff, Filter.mem_principal,
        Set.setOf_subset_setOf, Prod.forall]
      exact fun Îµ hÎµ f g h x hx â†¦ (le_iSupâ‚‚ (f := fun x _ â†¦ edist (f x) (g x)) x hx).trans h.le

end UniformOnFun
