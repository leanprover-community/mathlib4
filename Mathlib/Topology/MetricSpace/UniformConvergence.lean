import Mathlib.Topology.MetricSpace.Lipschitz
import Mathlib.Topology.UniformSpace.UniformConvergenceTopology

/-! # Metric conditions for continuity of functions into uniform convergence spaces.

If `X` and `Î²` are (pseudo) metric spaces, and `ğ”–` is a set of subsets of `Î±`,
then a function `f : X â†’ Î± â†’áµ¤[ğ”–] Î²` is continuous if for each `a âˆˆ s âˆˆ ğ”–`, the
function `x â†¦ f x a` is Lipschitz (with constant depending on `s`). In certain
contexts, this is a more convenient criterion for continuity of `f`.

Likewise, for `f : X â†’ Î± â†’áµ¤ Î²`, if `x â†¦ f x a` is Lipschitz for each fixed `a : Î±`
(with the same Lipschitz constant), then `f` is continuous.
-/

open scoped NNReal UniformConvergence

variable {X Î± Î² : Type*} {ğ”– : Set (Set Î±)} [PseudoMetricSpace X] [PseudoMetricSpace Î²]

/-- If `f : X â†’ Î± â†’áµ¤[ğ”–] Î²` is Lipschitz for each fixed `a âˆˆ s âˆˆ ğ”–`, with Lipschitz
constant depending on `s`, then `f` is continuous. -/
lemma UniformOnFun.continuous_of_lipschitzWith {f : X â†’ Î± â†’áµ¤[ğ”–] Î²}
    (C : Set Î± â†’ â„â‰¥0) (hf : âˆ€ s âˆˆ ğ”–, âˆ€ a âˆˆ s, LipschitzWith (C s) (fun x â†¦ toFun _ (f x) a)) :
    Continuous f := by
  simp_rw [continuous_iff_continuousAt, ContinuousAt, UniformOnFun.tendsto_iff_tendstoUniformlyOn,
    Metric.nhds_basis_closedBall.tendstoUniformlyOn_iff Metric.uniformity_basis_dist_le]
  refine fun xâ‚€ s hs Îµ hÎµ â†¦ âŸ¨Îµ / (C s + 1), by positivity, fun x hx a ha â†¦ ?_âŸ©
  simp only [Metric.mem_closedBall, dist_comm x, Function.comp_apply, Set.mem_setOf_eq] at hx âŠ¢
  calc
    _ â‰¤ C s * dist xâ‚€ x := (hf s hs a ha).dist_le_mul _ _
    _ â‰¤ C s * (Îµ / (C s + 1)) := by gcongr
    _ â‰¤ Îµ := by
      rw [â† mul_div_assoc, div_le_iffâ‚€' (by positivity)]
      gcongr
      simp

/-- If `f : X â†’ Î± â†’áµ¤ Î²` is Lipschitz for each fixed `a âˆˆ s âˆˆ ğ”–`, with Lipschitz
constant depending on `s`, then `f` is continuous. -/
lemma UniformFun.continuous_of_lipschitzWith {f : X â†’ Î± â†’áµ¤ Î²} (C : â„â‰¥0)
    (hf : âˆ€ a, LipschitzWith C (fun x â†¦ toFun (f x) a)) :
    Continuous f := by
  let e := UniformOnFun.uniformEquivUniformFun (Î± := Î±) (ğ”– := {Set.univ}) Î² (by simp)
  rw [e.symm.isUniformInducing.isInducing.continuous_iff]
  apply UniformOnFun.continuous_of_lipschitzWith (fun _ â†¦ C)
  rintro - - a -
  exact hf a
