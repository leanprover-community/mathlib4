/-
Copyright (c) 2022 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Topology.MetricSpace.PiNat
import Mathlib.Topology.Metrizable.CompletelyMetrizable
import Mathlib.Topology.Sets.Opens

/-!
# Polish spaces

A topological space is Polish if its topology is second-countable and there exists a compatible
complete metric. This is the class of spaces that is well-behaved with respect to measure theory.
In this file, we establish the basic properties of Polish spaces.

## Main definitions and results

* `PolishSpace Œ±` is a mixin typeclass on a topological space, requiring that the topology is
  second-countable and compatible with a complete metric. To endow the space with such a metric,
  use in a proof `letI := upgradeIsCompletelyMetrizable Œ±`.
* `IsClosed.polishSpace`: a closed subset of a Polish space is Polish.
* `IsOpen.polishSpace`: an open subset of a Polish space is Polish.
* `exists_nat_nat_continuous_surjective`: any nonempty Polish space is the continuous image
  of the fundamental Polish space `‚Ñï ‚Üí ‚Ñï`.

A fundamental property of Polish spaces is that one can put finer topologies, still Polish,
with additional properties:

* `exists_polishSpace_forall_le`: on a topological space, consider countably many topologies
  `t n`, all Polish and finer than the original topology. Then there exists another Polish
  topology which is finer than all the `t n`.
* `IsClopenable s` is a property of a subset `s` of a topological space, requiring that there
  exists a finer topology, which is Polish, for which `s` becomes open and closed. We show that
  this property is satisfied for open sets, closed sets, for complements, and for countable unions.
  Once Borel-measurable sets are defined in later files, it will follow that any Borel-measurable
  set is clopenable. Once the Lusin-Souslin theorem is proved using analytic sets, we will even
  show that a set is clopenable if and only if it is Borel-measurable, see
  `isClopenable_iff_measurableSet`.
-/

noncomputable section

open Filter Function Metric TopologicalSpace Set Topology
open scoped Uniformity

variable {Œ± : Type*} {Œ≤ : Type*}

/-! ### Basic properties of Polish spaces -/


/-- A Polish space is a topological space with second countable topology, that can be endowed
with a metric for which it is complete.

To endow a Polish space with a complete metric space structure, do
`letI := upgradeIsCompletelyMetrizable Œ±`.
-/
class PolishSpace (Œ± : Type*) [h : TopologicalSpace Œ±] : Prop
    extends SecondCountableTopology Œ±, IsCompletelyMetrizableSpace Œ±

instance [TopologicalSpace Œ±] [SeparableSpace Œ±] [IsCompletelyMetrizableSpace Œ±] :
    PolishSpace Œ± := by
  letI := upgradeIsCompletelyMetrizable Œ±
  haveI := UniformSpace.secondCountable_of_separable Œ±
  constructor

namespace PolishSpace

/-- Any nonempty Polish space is the continuous image of the fundamental space `‚Ñï ‚Üí ‚Ñï`. -/
theorem exists_nat_nat_continuous_surjective (Œ± : Type*) [TopologicalSpace Œ±] [PolishSpace Œ±]
    [Nonempty Œ±] : ‚àÉ f : (‚Ñï ‚Üí ‚Ñï) ‚Üí Œ±, Continuous f ‚àß Surjective f :=
  letI := upgradeIsCompletelyMetrizable Œ±
  exists_nat_nat_continuous_surjective_of_completeSpace Œ±

/-- Given a closed embedding into a Polish space, the source space is also Polish. -/
theorem _root_.Topology.IsClosedEmbedding.polishSpace [TopologicalSpace Œ±] [TopologicalSpace Œ≤]
    [PolishSpace Œ≤] {f : Œ± ‚Üí Œ≤} (hf : IsClosedEmbedding f) : PolishSpace Œ± := by
  letI := upgradeIsCompletelyMetrizable Œ≤
  letI : MetricSpace Œ± := hf.isEmbedding.comapMetricSpace f
  haveI : SecondCountableTopology Œ± := hf.isEmbedding.secondCountableTopology
  have : CompleteSpace Œ± := by
    rw [completeSpace_iff_isComplete_range hf.isEmbedding.to_isometry.isUniformInducing]
    exact hf.isClosed_range.isComplete
  infer_instance

/-- Pulling back a Polish topology under an equiv gives again a Polish topology. -/
theorem _root_.Equiv.polishSpace_induced [t : TopologicalSpace Œ≤] [PolishSpace Œ≤] (f : Œ± ‚âÉ Œ≤) :
    @PolishSpace Œ± (t.induced f) :=
  letI : TopologicalSpace Œ± := t.induced f
  (f.toHomeomorphOfIsInducing ‚ü®rfl‚ü©).isClosedEmbedding.polishSpace

/-- A closed subset of a Polish space is also Polish. -/
theorem _root_.IsClosed.polishSpace [TopologicalSpace Œ±] [PolishSpace Œ±] {s : Set Œ±}
    (hs : IsClosed s) : PolishSpace s :=
  hs.isClosedEmbedding_subtypeVal.polishSpace

protected theorem _root_.CompletePseudometrizable.iInf {Œπ : Type*} [Countable Œπ]
    {t : Œπ ‚Üí TopologicalSpace Œ±} (ht‚ÇÄ : ‚àÉ t‚ÇÄ, @T2Space Œ± t‚ÇÄ ‚àß ‚àÄ i, t i ‚â§ t‚ÇÄ)
    (ht : ‚àÄ i, ‚àÉ u : UniformSpace Œ±, CompleteSpace Œ± ‚àß ùì§[u].IsCountablyGenerated ‚àß
      u.toTopologicalSpace = t i) :
    ‚àÉ u : UniformSpace Œ±, CompleteSpace Œ± ‚àß
      ùì§[u].IsCountablyGenerated ‚àß u.toTopologicalSpace = ‚®Ö i, t i := by
  choose u hcomp hcount hut using ht
  obtain rfl : t = fun i ‚Ü¶ (u i).toTopologicalSpace := (funext hut).symm
  refine ‚ü®‚®Ö i, u i, .iInf hcomp ht‚ÇÄ, ?_, UniformSpace.toTopologicalSpace_iInf‚ü©
  rw [iInf_uniformity]
  infer_instance

protected theorem iInf {Œπ : Type*} [Countable Œπ] {t : Œπ ‚Üí TopologicalSpace Œ±}
    (ht‚ÇÄ : ‚àÉ i‚ÇÄ, ‚àÄ i, t i ‚â§ t i‚ÇÄ) (ht : ‚àÄ i, @PolishSpace Œ± (t i)) : @PolishSpace Œ± (‚®Ö i, t i) := by
  rcases ht‚ÇÄ with ‚ü®i‚ÇÄ, hi‚ÇÄ‚ü©
  rcases CompletePseudometrizable.iInf ‚ü®t i‚ÇÄ, letI := t i‚ÇÄ; haveI := ht i‚ÇÄ; inferInstance, hi‚ÇÄ‚ü©
    fun i ‚Ü¶
      letI := t i; haveI := ht i; letI := upgradeIsCompletelyMetrizable Œ±
      ‚ü®inferInstance, inferInstance, inferInstance, rfl‚ü©
    with ‚ü®u, hcomp, hcount, htop‚ü©
  rw [‚Üê htop]
  have : @SecondCountableTopology Œ± u.toTopologicalSpace :=
    htop.symm ‚ñ∏ secondCountableTopology_iInf fun i ‚Ü¶ letI := t i; (ht i).toSecondCountableTopology
  have : @T1Space Œ± u.toTopologicalSpace :=
    htop.symm ‚ñ∏ t1Space_antitone (iInf_le _ i‚ÇÄ) (by letI := t i‚ÇÄ; haveI := ht i‚ÇÄ; infer_instance)
  infer_instance

/-- Given a Polish space, and countably many finer Polish topologies, there exists another Polish
topology which is finer than all of them. -/
theorem exists_polishSpace_forall_le {Œπ : Type*} [Countable Œπ] [t : TopologicalSpace Œ±]
    [p : PolishSpace Œ±] (m : Œπ ‚Üí TopologicalSpace Œ±) (hm : ‚àÄ n, m n ‚â§ t)
    (h'm : ‚àÄ n, @PolishSpace Œ± (m n)) :
    ‚àÉ t' : TopologicalSpace Œ±, (‚àÄ n, t' ‚â§ m n) ‚àß t' ‚â§ t ‚àß @PolishSpace Œ± t' :=
  ‚ü®‚®Ö i : Option Œπ, i.elim t m, fun i ‚Ü¶ iInf_le _ (some i), iInf_le _ none,
    .iInf ‚ü®none, Option.forall.2 ‚ü®le_rfl, hm‚ü©‚ü© <| Option.forall.2 ‚ü®p, h'm‚ü©‚ü©

instance : PolishSpace ENNReal :=
  ENNReal.orderIsoUnitIntervalBirational.toHomeomorph.isClosedEmbedding.polishSpace

end PolishSpace

/-!
### An open subset of a Polish space is Polish

To prove this fact, one needs to construct another metric, giving rise to the same topology,
for which the open subset is complete. This is not obvious, as for instance `(0,1) ‚äÜ ‚Ñù` is not
complete for the usual metric of `‚Ñù`: one should build a new metric that blows up close to the
boundary.
-/

namespace TopologicalSpace.Opens

variable [MetricSpace Œ±] {s : Opens Œ±}

/-- A type synonym for a subset `s` of a metric space, on which we will construct another metric
for which it will be complete. -/
def CompleteCopy {Œ± : Type*} [MetricSpace Œ±] (s : Opens Œ±) : Type _ := s

namespace CompleteCopy

/-- A distance on an open subset `s` of a metric space, designed to make it complete.  It is given
by `dist' x y = dist x y + |1 / dist x s·∂ú - 1 / dist y s·∂ú|`, where the second term blows up close to
the boundary to ensure that Cauchy sequences for `dist'` remain well inside `s`. -/
instance instDist : Dist (CompleteCopy s) where
  dist x y := dist x.1 y.1 + abs (1 / infDist x.1 s·∂ú - 1 / infDist y.1 s·∂ú)

theorem dist_eq (x y : CompleteCopy s) :
    dist x y = dist x.1 y.1 + abs (1 / infDist x.1 s·∂ú - 1 / infDist y.1 s·∂ú) :=
  rfl

theorem dist_val_le_dist (x y : CompleteCopy s) : dist x.1 y.1 ‚â§ dist x y :=
  le_add_of_nonneg_right (abs_nonneg _)

instance : TopologicalSpace (CompleteCopy s) := inferInstanceAs (TopologicalSpace s)
instance [SecondCountableTopology Œ±] : SecondCountableTopology (CompleteCopy s) :=
  inferInstanceAs (SecondCountableTopology s)
instance : T0Space (CompleteCopy s) := inferInstanceAs (T0Space s)

/--
A metric space structure on a subset `s` of a metric space, designed to make it complete
if `s` is open. It is given by `dist' x y = dist x y + |1 / dist x s·∂ú - 1 / dist y s·∂ú|`, where the
second term blows up close to the boundary to ensure that Cauchy sequences for `dist'` remain well
inside `s`.

This definition ensures the `TopologicalSpace` structure on
`TopologicalSpace.Opens.CompleteCopy s` is definitionally equal to the original one.
-/
instance instMetricSpace : MetricSpace (CompleteCopy s) := by
  refine @MetricSpace.ofT0PseudoMetricSpace (CompleteCopy s)
    (.ofDistTopology dist (fun _ ‚Ü¶ ?_) (fun _ _ ‚Ü¶ ?_) (fun x y z ‚Ü¶ ?_) fun t ‚Ü¶ ?_) _
  ¬∑ simp only [dist_eq, dist_self, one_div, sub_self, abs_zero, add_zero]
  ¬∑ simp only [dist_eq, dist_comm, abs_sub_comm]
  ¬∑ calc
      dist x z = dist x.1 z.1 + |1 / infDist x.1 s·∂ú - 1 / infDist z.1 s·∂ú| := rfl
      _ ‚â§ dist x.1 y.1 + dist y.1 z.1 + (|1 / infDist x.1 s·∂ú - 1 / infDist y.1 s·∂ú| +
            |1 / infDist y.1 s·∂ú - 1 / infDist z.1 s·∂ú|) :=
        add_le_add (dist_triangle _ _ _) (dist_triangle (1 / infDist _ _) _ _)
      _ = dist x y + dist y z := add_add_add_comm ..
  ¬∑ refine ‚ü®fun h x hx ‚Ü¶ ?_, fun h ‚Ü¶ isOpen_iff_mem_nhds.2 fun x hx ‚Ü¶ ?_‚ü©
    ¬∑ rcases (Metric.isOpen_iff (Œ± := s)).1 h x hx with ‚ü®Œµ, Œµ0, hŒµ‚ü©
      exact ‚ü®Œµ, Œµ0, fun y hy ‚Ü¶ hŒµ <| (dist_comm _ _).trans_lt <| (dist_val_le_dist _ _).trans_lt hy‚ü©
    ¬∑ rcases h x hx with ‚ü®Œµ, Œµ0, hŒµ‚ü©
      simp only [dist_eq, one_div] at hŒµ
      have : Tendsto (fun y : s ‚Ü¶ dist x.1 y.1 + |(infDist x.1 s·∂ú)‚Åª¬π - (infDist y.1 s·∂ú)‚Åª¬π|)
          (ùìù x) (ùìù (dist x.1 x.1 + |(infDist x.1 s·∂ú)‚Åª¬π - (infDist x.1 s·∂ú)‚Åª¬π|)) := by
        refine (tendsto_const_nhds.dist continuous_subtype_val.continuousAt).add
          (tendsto_const_nhds.sub <| ?_).abs
        refine (continuousAt_inv_infDist_pt ?_).comp continuous_subtype_val.continuousAt
        rw [s.isOpen.isClosed_compl.closure_eq, mem_compl_iff, not_not]
        exact x.2
      simp only [dist_self, sub_self, abs_zero, zero_add] at this
      exact mem_of_superset (this <| gt_mem_nhds Œµ0) hŒµ

instance instCompleteSpace [CompleteSpace Œ±] : CompleteSpace (CompleteCopy s) := by
  refine Metric.complete_of_convergent_controlled_sequences ((1 / 2) ^ ¬∑) (by simp) fun u hu ‚Ü¶ ?_
  have A : CauchySeq fun n => (u n).1 := by
    refine cauchySeq_of_le_tendsto_0 (fun n : ‚Ñï => (1 / 2) ^ n) (fun n m N hNn hNm => ?_) ?_
    ¬∑ exact (dist_val_le_dist (u n) (u m)).trans (hu N n m hNn hNm).le
    ¬∑ exact tendsto_pow_atTop_nhds_zero_of_lt_one (by simp) (by norm_num)
  obtain ‚ü®x, xlim‚ü© : ‚àÉ x, Tendsto (fun n => (u n).1) atTop (ùìù x) := cauchySeq_tendsto_of_complete A
  by_cases xs : x ‚àà s
  ¬∑ exact ‚ü®‚ü®x, xs‚ü©, tendsto_subtype_rng.2 xlim‚ü©
  obtain ‚ü®C, hC‚ü© : ‚àÉ C, ‚àÄ n, 1 / infDist (u n).1 s·∂ú < C := by
    refine ‚ü®(1 / 2) ^ 0 + 1 / infDist (u 0).1 s·∂ú, fun n ‚Ü¶ ?_‚ü©
    rw [‚Üê sub_lt_iff_lt_add]
    calc
      _ ‚â§ |1 / infDist (u n).1 s·∂ú - 1 / infDist (u 0).1 s·∂ú| := le_abs_self _
      _ = |1 / infDist (u 0).1 s·∂ú - 1 / infDist (u n).1 s·∂ú| := abs_sub_comm _ _
      _ ‚â§ dist (u 0) (u n) := le_add_of_nonneg_left dist_nonneg
      _ < (1 / 2) ^ 0 := hu 0 0 n le_rfl n.zero_le
  have Cpos : 0 < C := lt_of_le_of_lt (div_nonneg zero_le_one infDist_nonneg) (hC 0)
  have Hmem : ‚àÄ {y}, y ‚àà s ‚Üî 0 < infDist y s·∂ú := fun {y} ‚Ü¶ by
    rw [‚Üê s.isOpen.isClosed_compl.notMem_iff_infDist_pos ‚ü®x, xs‚ü©]; exact not_not.symm
  have I : ‚àÄ n, 1 / C ‚â§ infDist (u n).1 s·∂ú := fun n ‚Ü¶ by
    have : 0 < infDist (u n).1 s·∂ú := Hmem.1 (u n).2
    rw [div_le_iff‚ÇÄ' Cpos]
    exact (div_le_iff‚ÇÄ this).1 (hC n).le
  have I' : 1 / C ‚â§ infDist x s·∂ú :=
    have : Tendsto (fun n => infDist (u n).1 s·∂ú) atTop (ùìù (infDist x s·∂ú)) :=
      ((continuous_infDist_pt (s·∂ú : Set Œ±)).tendsto x).comp xlim
    ge_of_tendsto' this I
  exact absurd (Hmem.2 <| lt_of_lt_of_le (div_pos one_pos Cpos) I') xs

/-- An open subset of a Polish space is also Polish. -/
theorem _root_.IsOpen.polishSpace {Œ± : Type*} [TopologicalSpace Œ±] [PolishSpace Œ±] {s : Set Œ±}
    (hs : IsOpen s) : PolishSpace s := by
  letI := upgradeIsCompletelyMetrizable Œ±
  lift s to Opens Œ± using hs
  exact inferInstanceAs (PolishSpace s.CompleteCopy)

end CompleteCopy

end TopologicalSpace.Opens

namespace PolishSpace

/-! ### Clopenable sets in Polish spaces -/

/-- A set in a topological space is clopenable if there exists a finer Polish topology for which
this set is open and closed. It turns out that this notion is equivalent to being Borel-measurable,
but this is nontrivial (see `isClopenable_iff_measurableSet`). -/
def IsClopenable [t : TopologicalSpace Œ±] (s : Set Œ±) : Prop :=
  ‚àÉ t' : TopologicalSpace Œ±, t' ‚â§ t ‚àß @PolishSpace Œ± t' ‚àß IsClosed[t'] s ‚àß IsOpen[t'] s

/-- Given a closed set `s` in a Polish space, one can construct a finer Polish topology for
which `s` is both open and closed. -/
theorem _root_.IsClosed.isClopenable [TopologicalSpace Œ±] [PolishSpace Œ±] {s : Set Œ±}
    (hs : IsClosed s) : IsClopenable s := by
  /- Both sets `s` and `s·∂ú` admit a Polish topology. So does their disjoint union `s ‚äï s·∂ú`.
    Pulling back this topology by the canonical bijection with `Œ±` gives the desired Polish
    topology in which `s` is both open and closed. -/
  classical
  haveI : PolishSpace s := hs.polishSpace
  let t : Set Œ± := s·∂ú
  haveI : PolishSpace t := hs.isOpen_compl.polishSpace
  let f : s ‚äï t ‚âÉ Œ± := Equiv.Set.sumCompl s
  have hle : TopologicalSpace.coinduced f instTopologicalSpaceSum ‚â§ ‚Äπ_‚Ä∫ := by
    simp only [instTopologicalSpaceSum, coinduced_sup, coinduced_compose, sup_le_iff,
      ‚Üê continuous_iff_coinduced_le]
    exact ‚ü®continuous_subtype_val, continuous_subtype_val‚ü©
  refine ‚ü®.coinduced f instTopologicalSpaceSum, hle, ?_, hs.mono hle, ?_‚ü©
  ¬∑ rw [‚Üê f.induced_symm]
    exact f.symm.polishSpace_induced
  ¬∑ rw [isOpen_coinduced, isOpen_sum_iff]
    simp only [preimage_preimage, f]
    have inl (x : s) : (Equiv.Set.sumCompl s) (Sum.inl x) = x := Equiv.Set.sumCompl_apply_inl ..
    have inr (x : ‚Üës·∂ú) : (Equiv.Set.sumCompl s) (Sum.inr x) = x := Equiv.Set.sumCompl_apply_inr ..
    simp_rw [t, inl, inr, Subtype.coe_preimage_self]
    simp only [isOpen_univ, true_and]
    rw [Subtype.preimage_coe_compl']
    simp

theorem IsClopenable.compl [TopologicalSpace Œ±] {s : Set Œ±} (hs : IsClopenable s) :
    IsClopenable s·∂ú := by
  rcases hs with ‚ü®t, t_le, t_polish, h, h'‚ü©
  exact ‚ü®t, t_le, t_polish, @IsOpen.isClosed_compl Œ± t s h', @IsClosed.isOpen_compl Œ± t s h‚ü©

theorem _root_.IsOpen.isClopenable [TopologicalSpace Œ±] [PolishSpace Œ±] {s : Set Œ±}
    (hs : IsOpen s) : IsClopenable s := by
  simpa using hs.isClosed_compl.isClopenable.compl

-- TODO: generalize for free to `[Countable Œπ] {s : Œπ ‚Üí Set Œ±}`
theorem IsClopenable.iUnion [t : TopologicalSpace Œ±] [PolishSpace Œ±] {s : ‚Ñï ‚Üí Set Œ±}
    (hs : ‚àÄ n, IsClopenable (s n)) : IsClopenable (‚ãÉ n, s n) := by
  choose m mt m_polish _ m_open using hs
  obtain ‚ü®t', t'm, -, t'_polish‚ü© :
      ‚àÉ t' : TopologicalSpace Œ±, (‚àÄ n : ‚Ñï, t' ‚â§ m n) ‚àß t' ‚â§ t ‚àß @PolishSpace Œ± t' :=
    exists_polishSpace_forall_le m mt m_polish
  have A : IsOpen[t'] (‚ãÉ n, s n) := by
    apply isOpen_iUnion
    intro n
    apply t'm n
    exact m_open n
  obtain ‚ü®t'', t''_le, t''_polish, h1, h2‚ü© : ‚àÉ t'' : TopologicalSpace Œ±,
      t'' ‚â§ t' ‚àß @PolishSpace Œ± t'' ‚àß IsClosed[t''] (‚ãÉ n, s n) ‚àß IsOpen[t''] (‚ãÉ n, s n) :=
    @IsOpen.isClopenable Œ± t' t'_polish _ A
  exact ‚ü®t'', t''_le.trans ((t'm 0).trans (mt 0)), t''_polish, h1, h2‚ü©

end PolishSpace
