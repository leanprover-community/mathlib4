/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Logic.Encodable.Pi
import Mathlib.SetTheory.Cardinal.Basic
import Mathlib.Topology.MetricSpace.Closeds
import Mathlib.Topology.MetricSpace.Completion
import Mathlib.Topology.MetricSpace.GromovHausdorffRealized
import Mathlib.Topology.MetricSpace.Kuratowski

/-!
# Gromov-Hausdorff distance

This file defines the Gromov-Hausdorff distance on the space of nonempty compact metric spaces
up to isometry.

We introduce the space of all nonempty compact metric spaces, up to isometry,
called `GHSpace`, and endow it with a metric space structure. The distance,
known as the Gromov-Hausdorff distance, is defined as follows: given two
nonempty compact spaces `X` and `Y`, their distance is the minimum Hausdorff distance
between all possible isometric embeddings of `X` and `Y` in all metric spaces.
To define properly the Gromov-Hausdorff space, we consider the non-empty
compact subsets of `‚Ñì^‚àû(‚Ñù)` up to isometry, which is a well-defined type,
and define the distance as the infimum of the Hausdorff distance over all
embeddings in `‚Ñì^‚àû(‚Ñù)`. We prove that this coincides with the previous description,
as all separable metric spaces embed isometrically into `‚Ñì^‚àû(‚Ñù)`, through an
embedding called the Kuratowski embedding.
To prove that we have a distance, we should show that if spaces can be coupled
to be arbitrarily close, then they are isometric. More generally, the Gromov-Hausdorff
distance is realized, i.e., there is a coupling for which the Hausdorff distance
is exactly the Gromov-Hausdorff distance. This follows from a compactness
argument, essentially following from Arzela-Ascoli.

## Main results

We prove the most important properties of the Gromov-Hausdorff space: it is a polish space,
i.e., it is complete and second countable. We also prove the Gromov compactness criterion.

-/

noncomputable section

open scoped Topology ENNReal Cardinal
open Set Function TopologicalSpace Filter Metric Quotient Bornology
open BoundedContinuousFunction Nat Int kuratowskiEmbedding

open Sum (inl inr)

local notation "‚Ñì_infty_‚Ñù" => lp (fun n : ‚Ñï => ‚Ñù) ‚àû

universe u v w

attribute [local instance] metricSpaceSum

namespace GromovHausdorff

/-! In this section, we define the Gromov-Hausdorff space, denoted `GHSpace` as the quotient
of nonempty compact subsets of `‚Ñì^‚àû(‚Ñù)` by identifying isometric sets.
Using the Kuratwoski embedding, we get a canonical map `toGHSpace` mapping any nonempty
compact type to `GHSpace`. -/
section GHSpace

/-- Equivalence relation identifying two nonempty compact sets which are isometric -/
private def IsometryRel (x : NonemptyCompacts ‚Ñì_infty_‚Ñù) (y : NonemptyCompacts ‚Ñì_infty_‚Ñù) : Prop :=
  Nonempty (x ‚âÉ·µ¢ y)

/-- This is indeed an equivalence relation -/
private theorem equivalence_isometryRel : Equivalence IsometryRel :=
  ‚ü®fun _ => Nonempty.intro (IsometryEquiv.refl _), fun ‚ü®e‚ü© => ‚ü®e.symm‚ü©, fun ‚ü®e‚ü© ‚ü®f‚ü© => ‚ü®e.trans f‚ü©‚ü©

/-- setoid instance identifying two isometric nonempty compact subspaces of ‚Ñì^‚àû(‚Ñù) -/
instance IsometryRel.setoid : Setoid (NonemptyCompacts ‚Ñì_infty_‚Ñù) :=
  Setoid.mk IsometryRel equivalence_isometryRel

/-- The Gromov-Hausdorff space -/
def GHSpace : Type :=
  Quotient IsometryRel.setoid

/-- Map any nonempty compact type to `GHSpace` -/
def toGHSpace (X : Type u) [MetricSpace X] [CompactSpace X] [Nonempty X] : GHSpace :=
  ‚ü¶NonemptyCompacts.kuratowskiEmbedding X‚üß

instance : Inhabited GHSpace :=
  ‚ü®Quot.mk _ ‚ü®‚ü®{0}, isCompact_singleton‚ü©, singleton_nonempty _‚ü©‚ü©

/-- A metric space representative of any abstract point in `GHSpace` -/
def GHSpace.Rep (p : GHSpace) : Type :=
  (Quotient.out p : NonemptyCompacts ‚Ñì_infty_‚Ñù)

theorem eq_toGHSpace_iff {X : Type u} [MetricSpace X] [CompactSpace X] [Nonempty X]
    {p : NonemptyCompacts ‚Ñì_infty_‚Ñù} :
    ‚ü¶p‚üß = toGHSpace X ‚Üî ‚àÉ Œ® : X ‚Üí ‚Ñì_infty_‚Ñù, Isometry Œ® ‚àß range Œ® = p := by
  simp only [toGHSpace, Quotient.eq]
  refine ‚ü®fun h => ?_, ?_‚ü©
  ¬∑ rcases Setoid.symm h with ‚ü®e‚ü©
    have f := (kuratowskiEmbedding.isometry X).isometryEquivOnRange.trans e
    use fun x => f x, isometry_subtype_coe.comp f.isometry
    rw [range_comp', f.range_eq_univ, Set.image_univ, Subtype.range_coe]
  ¬∑ rintro ‚ü®Œ®, ‚ü®isomŒ®, rangeŒ®‚ü©‚ü©
    have f :=
      ((kuratowskiEmbedding.isometry X).isometryEquivOnRange.symm.trans
          isomŒ®.isometryEquivOnRange).symm
    have E : (range Œ® ‚âÉ·µ¢ NonemptyCompacts.kuratowskiEmbedding X)
        = (p ‚âÉ·µ¢ range (kuratowskiEmbedding X)) := by
      dsimp only [NonemptyCompacts.kuratowskiEmbedding]; rw [rangeŒ®]; rfl
    exact ‚ü®cast E f‚ü©

theorem eq_toGHSpace {p : NonemptyCompacts ‚Ñì_infty_‚Ñù} : ‚ü¶p‚üß = toGHSpace p :=
  eq_toGHSpace_iff.2 ‚ü®fun x => x, isometry_subtype_coe, Subtype.range_coe‚ü©

section

instance repGHSpaceMetricSpace {p : GHSpace} : MetricSpace p.Rep :=
  inferInstanceAs <| MetricSpace p.out

instance rep_gHSpace_compactSpace {p : GHSpace} : CompactSpace p.Rep :=
  inferInstanceAs <| CompactSpace p.out

instance rep_gHSpace_nonempty {p : GHSpace} : Nonempty p.Rep :=
  inferInstanceAs <| Nonempty p.out

end

theorem GHSpace.toGHSpace_rep (p : GHSpace) : toGHSpace p.Rep = p := by
  change toGHSpace (Quot.out p : NonemptyCompacts ‚Ñì_infty_‚Ñù) = p
  rw [‚Üê eq_toGHSpace]
  exact Quot.out_eq p

/-- Two nonempty compact spaces have the same image in `GHSpace` if and only if they are
isometric. -/
theorem toGHSpace_eq_toGHSpace_iff_isometryEquiv {X : Type u} [MetricSpace X] [CompactSpace X]
    [Nonempty X] {Y : Type v} [MetricSpace Y] [CompactSpace Y] [Nonempty Y] :
    toGHSpace X = toGHSpace Y ‚Üî Nonempty (X ‚âÉ·µ¢ Y) :=
  ‚ü®by
    simp only [toGHSpace]
    rw [Quotient.eq]
    rintro ‚ü®e‚ü©
    have I :
      (NonemptyCompacts.kuratowskiEmbedding X ‚âÉ·µ¢ NonemptyCompacts.kuratowskiEmbedding Y) =
        (range (kuratowskiEmbedding X) ‚âÉ·µ¢ range (kuratowskiEmbedding Y)) := by
      dsimp only [NonemptyCompacts.kuratowskiEmbedding]; rfl
    have f := (kuratowskiEmbedding.isometry X).isometryEquivOnRange
    have g := (kuratowskiEmbedding.isometry Y).isometryEquivOnRange.symm
    exact ‚ü®f.trans <| (cast I e).trans g‚ü©, by
    rintro ‚ü®e‚ü©
    simp only [toGHSpace, Quotient.eq']
    have f := (kuratowskiEmbedding.isometry X).isometryEquivOnRange.symm
    have g := (kuratowskiEmbedding.isometry Y).isometryEquivOnRange
    have I :
      (range (kuratowskiEmbedding X) ‚âÉ·µ¢ range (kuratowskiEmbedding Y)) =
        (NonemptyCompacts.kuratowskiEmbedding X ‚âÉ·µ¢ NonemptyCompacts.kuratowskiEmbedding Y) := by
      dsimp only [NonemptyCompacts.kuratowskiEmbedding]; rfl
    rw [Quotient.eq]
    exact ‚ü®cast I ((f.trans e).trans g)‚ü©‚ü©

/-- Distance on `GHSpace`: the distance between two nonempty compact spaces is the infimum
Hausdorff distance between isometric copies of the two spaces in a metric space. For the definition,
we only consider embeddings in `‚Ñì^‚àû(‚Ñù)`, but we will prove below that it works for all spaces. -/
instance : Dist GHSpace where
  dist x y := sInf <| (fun p : NonemptyCompacts ‚Ñì_infty_‚Ñù √ó NonemptyCompacts ‚Ñì_infty_‚Ñù =>
    hausdorffDist (p.1 : Set ‚Ñì_infty_‚Ñù) p.2) '' { a | ‚ü¶a‚üß = x } √óÀ¢ { b | ‚ü¶b‚üß = y }

/-- The Gromov-Hausdorff distance between two nonempty compact metric spaces, equal by definition to
the distance of the equivalence classes of these spaces in the Gromov-Hausdorff space. -/
def ghDist (X : Type u) (Y : Type v) [MetricSpace X] [Nonempty X] [CompactSpace X] [MetricSpace Y]
    [Nonempty Y] [CompactSpace Y] : ‚Ñù :=
  dist (toGHSpace X) (toGHSpace Y)

theorem dist_ghDist (p q : GHSpace) : dist p q = ghDist p.Rep q.Rep := by
  rw [ghDist, p.toGHSpace_rep, q.toGHSpace_rep]

/-- The Gromov-Hausdorff distance between two spaces is bounded by the Hausdorff distance
of isometric copies of the spaces, in any metric space. -/
theorem ghDist_le_hausdorffDist {X : Type u} [MetricSpace X] [CompactSpace X] [Nonempty X]
    {Y : Type v} [MetricSpace Y] [CompactSpace Y] [Nonempty Y] {Œ≥ : Type w} [MetricSpace Œ≥]
    {Œ¶ : X ‚Üí Œ≥} {Œ® : Y ‚Üí Œ≥} (ha : Isometry Œ¶) (hb : Isometry Œ®) :
    ghDist X Y ‚â§ hausdorffDist (range Œ¶) (range Œ®) := by
  /- For the proof, we want to embed `Œ≥` in `‚Ñì^‚àû(‚Ñù)`, to say that the Hausdorff distance is realized
    in `‚Ñì^‚àû(‚Ñù)` and therefore bounded below by the Gromov-Hausdorff-distance. However, `Œ≥` is not
    separable in general. We restrict to the union of the images of `X` and `Y` in `Œ≥`, which is
    separable and therefore embeddable in `‚Ñì^‚àû(‚Ñù)`. -/
  rcases exists_mem_of_nonempty X with ‚ü®xX, _‚ü©
  let s : Set Œ≥ := range Œ¶ ‚à™ range Œ®
  let Œ¶' : X ‚Üí Subtype s := fun y => ‚ü®Œ¶ y, mem_union_left _ (mem_range_self _)‚ü©
  let Œ®' : Y ‚Üí Subtype s := fun y => ‚ü®Œ® y, mem_union_right _ (mem_range_self _)‚ü©
  have IŒ¶' : Isometry Œ¶' := fun x y => ha x y
  have IŒ®' : Isometry Œ®' := fun x y => hb x y
  have : IsCompact s := (isCompact_range ha.continuous).union (isCompact_range hb.continuous)
  let _ : MetricSpace (Subtype s) := by infer_instance
  have : CompactSpace (Subtype s) := ‚ü®isCompact_iff_isCompact_univ.1 ‚ÄπIsCompact s‚Ä∫‚ü©
  have Œ¶Œ¶' : Œ¶ = Subtype.val ‚àò Œ¶' := rfl
  have Œ®Œ®' : Œ® = Subtype.val ‚àò Œ®' := rfl
  have : hausdorffDist (range Œ¶) (range Œ®) = hausdorffDist (range Œ¶') (range Œ®') := by
    rw [Œ¶Œ¶', Œ®Œ®', range_comp, range_comp]
    exact hausdorffDist_image isometry_subtype_coe
  rw [this]
  -- Embed `s` in `‚Ñì^‚àû(‚Ñù)` through its Kuratowski embedding
  let F := kuratowskiEmbedding (Subtype s)
  have : hausdorffDist (F '' range Œ¶') (F '' range Œ®') = hausdorffDist (range Œ¶') (range Œ®') :=
    hausdorffDist_image (kuratowskiEmbedding.isometry _)
  rw [‚Üê this]
  -- Let `A` and `B` be the images of `X` and `Y` under this embedding. They are in `‚Ñì^‚àû(‚Ñù)`, and
  -- their Hausdorff distance is the same as in the original space.
  let A : NonemptyCompacts ‚Ñì_infty_‚Ñù :=
    ‚ü®‚ü®F '' range Œ¶',
        (isCompact_range IŒ¶'.continuous).image (kuratowskiEmbedding.isometry _).continuous‚ü©,
      (range_nonempty _).image _‚ü©
  let B : NonemptyCompacts ‚Ñì_infty_‚Ñù :=
    ‚ü®‚ü®F '' range Œ®',
        (isCompact_range IŒ®'.continuous).image (kuratowskiEmbedding.isometry _).continuous‚ü©,
      (range_nonempty _).image _‚ü©
  have AX : ‚ü¶A‚üß = toGHSpace X := by
    rw [eq_toGHSpace_iff]
    exact ‚ü®fun x => F (Œ¶' x), (kuratowskiEmbedding.isometry _).comp IŒ¶', range_comp _ _‚ü©
  have BY : ‚ü¶B‚üß = toGHSpace Y := by
    rw [eq_toGHSpace_iff]
    exact ‚ü®fun x => F (Œ®' x), (kuratowskiEmbedding.isometry _).comp IŒ®', range_comp _ _‚ü©
  refine csInf_le ‚ü®0, ?_‚ü© ?_
  ¬∑ simp only [lowerBounds, mem_image, mem_prod, mem_setOf_eq, Prod.exists, and_imp,
      forall_exists_index]
    intro t _ _ _ _ ht
    rw [‚Üê ht]
    exact hausdorffDist_nonneg
  apply (mem_image _ _ _).2
  exists (‚ü®A, B‚ü© : NonemptyCompacts ‚Ñì_infty_‚Ñù √ó NonemptyCompacts ‚Ñì_infty_‚Ñù)

/-- The optimal coupling constructed above realizes exactly the Gromov-Hausdorff distance,
essentially by design. -/
theorem hausdorffDist_optimal {X : Type u} [MetricSpace X] [CompactSpace X] [Nonempty X]
    {Y : Type v} [MetricSpace Y] [CompactSpace Y] [Nonempty Y] :
    hausdorffDist (range (optimalGHInjl X Y)) (range (optimalGHInjr X Y)) = ghDist X Y := by
  inhabit X; inhabit Y
  /- we only need to check the inequality `‚â§`, as the other one follows from the previous lemma.
       As the Gromov-Hausdorff distance is an infimum, we need to check that the Hausdorff distance
       in the optimal coupling is smaller than the Hausdorff distance of any coupling.
       First, we check this for couplings which already have small Hausdorff distance: in this
       case, the induced "distance" on `X ‚äï Y` belongs to the candidates family introduced in the
       definition of the optimal coupling, and the conclusion follows from the optimality
       of the optimal coupling within this family.
    -/
  have A :
    ‚àÄ p q : NonemptyCompacts ‚Ñì_infty_‚Ñù,
      ‚ü¶p‚üß = toGHSpace X ‚Üí
        ‚ü¶q‚üß = toGHSpace Y ‚Üí
          hausdorffDist (p : Set ‚Ñì_infty_‚Ñù) q < diam (univ : Set X) + 1 + diam (univ : Set Y) ‚Üí
            hausdorffDist (range (optimalGHInjl X Y)) (range (optimalGHInjr X Y)) ‚â§
              hausdorffDist (p : Set ‚Ñì_infty_‚Ñù) q := by
    intro p q hp hq bound
    rcases eq_toGHSpace_iff.1 hp with ‚ü®Œ¶, ‚ü®Œ¶isom, Œ¶range‚ü©‚ü©
    rcases eq_toGHSpace_iff.1 hq with ‚ü®Œ®, ‚ü®Œ®isom, Œ®range‚ü©‚ü©
    have I : diam (range Œ¶ ‚à™ range Œ®) ‚â§ 2 * diam (univ : Set X) + 1 + 2 * diam (univ : Set Y) := by
      rcases exists_mem_of_nonempty X with ‚ü®xX, _‚ü©
      have : ‚àÉ y ‚àà range Œ®, dist (Œ¶ xX) y < diam (univ : Set X) + 1 + diam (univ : Set Y) := by
        rw [Œ®range]
        have : Œ¶ xX ‚àà (p : Set _) := Œ¶range ‚ñ∏ (mem_range_self _)
        exact
          exists_dist_lt_of_hausdorffDist_lt this bound
            (hausdorffEdist_ne_top_of_nonempty_of_bounded p.nonempty q.nonempty
              p.isCompact.isBounded q.isCompact.isBounded)
      rcases this with ‚ü®y, hy, dy‚ü©
      rcases mem_range.1 hy with ‚ü®z, hzy‚ü©
      rw [‚Üê hzy] at dy
      have DŒ¶ : diam (range Œ¶) = diam (univ : Set X) := Œ¶isom.diam_range
      have DŒ® : diam (range Œ®) = diam (univ : Set Y) := Œ®isom.diam_range
      calc
        diam (range Œ¶ ‚à™ range Œ®) ‚â§ diam (range Œ¶) + dist (Œ¶ xX) (Œ® z) + diam (range Œ®) :=
          diam_union (mem_range_self _) (mem_range_self _)
        _ ‚â§
            diam (univ : Set X) + (diam (univ : Set X) + 1 + diam (univ : Set Y)) +
              diam (univ : Set Y) := by
          rw [DŒ¶, DŒ®]
          gcongr
        _ = 2 * diam (univ : Set X) + 1 + 2 * diam (univ : Set Y) := by ring
    let f : X ‚äï Y ‚Üí ‚Ñì_infty_‚Ñù := fun x =>
      match x with
      | inl y => Œ¶ y
      | inr z => Œ® z
    let F : (X ‚äï Y) √ó (X ‚äï Y) ‚Üí ‚Ñù := fun p => dist (f p.1) (f p.2)
    -- check that the induced "distance" is a candidate
    have Fgood : F ‚àà candidates X Y := by
      simp only [F, candidates, forall_const, add_comm, eq_self_iff_true,
        dist_eq_zero, and_self_iff, Set.mem_setOf_eq]
      repeat' constructor
      ¬∑ exact fun x y =>
          calc
            F (inl x, inl y) = dist (Œ¶ x) (Œ¶ y) := rfl
            _ = dist x y := Œ¶isom.dist_eq x y
      ¬∑ exact fun x y =>
          calc
            F (inr x, inr y) = dist (Œ® x) (Œ® y) := rfl
            _ = dist x y := Œ®isom.dist_eq x y
      ¬∑ exact fun x y => dist_comm _ _
      ¬∑ exact fun x y z => dist_triangle _ _ _
      ¬∑ exact fun x y =>
          calc
            F (x, y) ‚â§ diam (range Œ¶ ‚à™ range Œ®) := by
              have A : ‚àÄ z : X ‚äï Y, f z ‚àà range Œ¶ ‚à™ range Œ® := by
                intro z
                cases z
                ¬∑ apply mem_union_left; apply mem_range_self
                ¬∑ apply mem_union_right; apply mem_range_self
              refine dist_le_diam_of_mem ?_ (A _) (A _)
              rw [Œ¶range, Œ®range]
              exact (p ‚äî q).isCompact.isBounded
            _ ‚â§ 2 * diam (univ : Set X) + 1 + 2 * diam (univ : Set Y) := I
    let Fb := candidatesBOfCandidates F Fgood
    have : hausdorffDist (range (optimalGHInjl X Y)) (range (optimalGHInjr X Y)) ‚â§ HD Fb :=
      hausdorffDist_optimal_le_HD _ _ (candidatesBOfCandidates_mem F Fgood)
    refine le_trans this (le_of_forall_gt_imp_ge_of_dense fun r hr => ?_)
    have I1 : ‚àÄ x : X, (‚®Ö y, Fb (inl x, inr y)) ‚â§ r := by
      intro x
      have : f (inl x) ‚àà (p : Set _) := Œ¶range ‚ñ∏ (mem_range_self _)
      rcases exists_dist_lt_of_hausdorffDist_lt this hr
          (hausdorffEdist_ne_top_of_nonempty_of_bounded p.nonempty q.nonempty p.isCompact.isBounded
            q.isCompact.isBounded) with
        ‚ü®z, zq, hz‚ü©
      have : z ‚àà range Œ® := by rwa [‚Üê Œ®range] at zq
      rcases mem_range.1 this with ‚ü®y, hy‚ü©
      calc
        (‚®Ö y, Fb (inl x, inr y)) ‚â§ Fb (inl x, inr y) :=
          ciInf_le (by simpa only [add_zero] using HD_below_aux1 0) y
        _ = dist (Œ¶ x) (Œ® y) := rfl
        _ = dist (f (inl x)) z := by rw [hy]
        _ ‚â§ r := le_of_lt hz
    have I2 : ‚àÄ y : Y, (‚®Ö x, Fb (inl x, inr y)) ‚â§ r := by
      intro y
      have : f (inr y) ‚àà (q : Set _) := Œ®range ‚ñ∏ (mem_range_self _)
      rcases exists_dist_lt_of_hausdorffDist_lt' this hr
          (hausdorffEdist_ne_top_of_nonempty_of_bounded p.nonempty q.nonempty p.isCompact.isBounded
            q.isCompact.isBounded) with
        ‚ü®z, zq, hz‚ü©
      have : z ‚àà range Œ¶ := by rwa [‚Üê Œ¶range] at zq
      rcases mem_range.1 this with ‚ü®x, hx‚ü©
      calc
        (‚®Ö x, Fb (inl x, inr y)) ‚â§ Fb (inl x, inr y) :=
          ciInf_le (by simpa only [add_zero] using HD_below_aux2 0) x
        _ = dist (Œ¶ x) (Œ® y) := rfl
        _ = dist z (f (inr y)) := by rw [hx]
        _ ‚â§ r := le_of_lt hz
    simp only [HD, ciSup_le I1, ciSup_le I2, max_le_iff, and_self_iff]
  /- Get the same inequality for any coupling. If the coupling is quite good, the desired
    inequality has been proved above. If it is bad, then the inequality is obvious. -/
  have B :
    ‚àÄ p q : NonemptyCompacts ‚Ñì_infty_‚Ñù,
      ‚ü¶p‚üß = toGHSpace X ‚Üí
        ‚ü¶q‚üß = toGHSpace Y ‚Üí
          hausdorffDist (range (optimalGHInjl X Y)) (range (optimalGHInjr X Y)) ‚â§
            hausdorffDist (p : Set ‚Ñì_infty_‚Ñù) q := by
    intro p q hp hq
    by_cases h :
      hausdorffDist (p : Set ‚Ñì_infty_‚Ñù) q < diam (univ : Set X) + 1 + diam (univ : Set Y)
    ¬∑ exact A p q hp hq h
    ¬∑ calc
        hausdorffDist (range (optimalGHInjl X Y)) (range (optimalGHInjr X Y)) ‚â§
            HD (candidatesBDist X Y) :=
          hausdorffDist_optimal_le_HD _ _ candidatesBDist_mem_candidatesB
        _ ‚â§ diam (univ : Set X) + 1 + diam (univ : Set Y) := HD_candidatesBDist_le
        _ ‚â§ hausdorffDist (p : Set ‚Ñì_infty_‚Ñù) q := not_lt.1 h
  refine le_antisymm ?_ ?_
  ¬∑ apply le_csInf
    ¬∑ refine (Set.Nonempty.prod ?_ ?_).image _ <;> exact ‚ü®_, rfl‚ü©
    ¬∑ rintro b ‚ü®‚ü®p, q‚ü©, ‚ü®hp, hq‚ü©, rfl‚ü©
      exact B p q hp hq
  ¬∑ exact ghDist_le_hausdorffDist (isometry_optimalGHInjl X Y) (isometry_optimalGHInjr X Y)

/-- The Gromov-Hausdorff distance can also be realized by a coupling in `‚Ñì^‚àû(‚Ñù)`, by embedding
the optimal coupling through its Kuratowski embedding. -/
theorem ghDist_eq_hausdorffDist (X : Type u) [MetricSpace X] [CompactSpace X] [Nonempty X]
    (Y : Type v) [MetricSpace Y] [CompactSpace Y] [Nonempty Y] :
    ‚àÉ Œ¶ : X ‚Üí ‚Ñì_infty_‚Ñù,
      ‚àÉ Œ® : Y ‚Üí ‚Ñì_infty_‚Ñù,
        Isometry Œ¶ ‚àß Isometry Œ® ‚àß ghDist X Y = hausdorffDist (range Œ¶) (range Œ®) := by
  let F := kuratowskiEmbedding (OptimalGHCoupling X Y)
  let Œ¶ := F ‚àò optimalGHInjl X Y
  let Œ® := F ‚àò optimalGHInjr X Y
  refine ‚ü®Œ¶, Œ®, ?_, ?_, ?_‚ü©
  ¬∑ exact (kuratowskiEmbedding.isometry _).comp (isometry_optimalGHInjl X Y)
  ¬∑ exact (kuratowskiEmbedding.isometry _).comp (isometry_optimalGHInjr X Y)
  ¬∑ rw [‚Üê image_univ, ‚Üê image_univ, image_comp F, image_univ, image_comp F (optimalGHInjr X Y),
      image_univ, ‚Üê hausdorffDist_optimal]
    exact (hausdorffDist_image (kuratowskiEmbedding.isometry _)).symm

/-- The Gromov-Hausdorff distance defines a genuine distance on the Gromov-Hausdorff space. -/
instance : MetricSpace GHSpace where
  dist := dist
  dist_self x := by
    rcases exists_rep x with ‚ü®y, hy‚ü©
    refine le_antisymm ?_ ?_
    ¬∑ apply csInf_le
      ¬∑ exact ‚ü®0, by rintro b ‚ü®‚ü®u, v‚ü©, -, rfl‚ü©; exact hausdorffDist_nonneg‚ü©
      ¬∑ simp only [mem_image, mem_prod, mem_setOf_eq, Prod.exists]
        exists y, y
        simpa only [and_self_iff, hausdorffDist_self_zero, eq_self_iff_true, and_true]
    ¬∑ apply le_csInf
      ¬∑ exact Set.Nonempty.image _ <| Set.Nonempty.prod ‚ü®y, hy‚ü© ‚ü®y, hy‚ü©
      ¬∑ rintro b ‚ü®‚ü®u, v‚ü©, -, rfl‚ü©; exact hausdorffDist_nonneg
  dist_comm x y := by
    have A :
      (fun p : NonemptyCompacts ‚Ñì_infty_‚Ñù √ó NonemptyCompacts ‚Ñì_infty_‚Ñù =>
            hausdorffDist (p.1 : Set ‚Ñì_infty_‚Ñù) p.2) ''
          { a | ‚ü¶a‚üß = x } √óÀ¢ { b | ‚ü¶b‚üß = y } =
        (fun p : NonemptyCompacts ‚Ñì_infty_‚Ñù √ó NonemptyCompacts ‚Ñì_infty_‚Ñù =>
              hausdorffDist (p.1 : Set ‚Ñì_infty_‚Ñù) p.2) ‚àò
            Prod.swap ''
          { a | ‚ü¶a‚üß = x } √óÀ¢ { b | ‚ü¶b‚üß = y } := by
      funext
      simp only [comp_apply, Prod.fst_swap, Prod.snd_swap]
      congr
      -- The next line had `singlePass := true` before https://github.com/leanprover-community/mathlib4/pull/9928,
      -- then was changed to be `simp only [hausdorffDist_comm]`,
      -- then `singlePass := true` was readded in https://github.com/leanprover-community/mathlib4/pull/8386 because of timeouts.
      -- TODO: figure out what causes the slowdown and make it a `simp only` again?
      simp +singlePass only [hausdorffDist_comm]
    simp only [dist, A, image_comp, image_swap_prod]
  eq_of_dist_eq_zero {x} {y} hxy := by
    /- To show that two spaces at zero distance are isometric,
       we argue that the distance is realized by some coupling.
        In this coupling, the two spaces are at zero Hausdorff distance,
        i.e., they coincide. Therefore, the original spaces are isometric. -/
    rcases ghDist_eq_hausdorffDist x.Rep y.Rep with ‚ü®Œ¶, Œ®, Œ¶isom, Œ®isom, DŒ¶Œ®‚ü©
    rw [‚Üê dist_ghDist, hxy] at DŒ¶Œ®
    have : range Œ¶ = range Œ® := by
      have hŒ¶ : IsCompact (range Œ¶) := isCompact_range Œ¶isom.continuous
      have hŒ® : IsCompact (range Œ®) := isCompact_range Œ®isom.continuous
      apply (IsClosed.hausdorffDist_zero_iff_eq _ _ _).1 DŒ¶Œ®.symm
      ¬∑ exact hŒ¶.isClosed
      ¬∑ exact hŒ®.isClosed
      ¬∑ exact hausdorffEdist_ne_top_of_nonempty_of_bounded (range_nonempty _) (range_nonempty _)
          hŒ¶.isBounded hŒ®.isBounded
    have T : (range Œ® ‚âÉ·µ¢ y.Rep) = (range Œ¶ ‚âÉ·µ¢ y.Rep) := by rw [this]
    have eŒ® := cast T Œ®isom.isometryEquivOnRange.symm
    have e := Œ¶isom.isometryEquivOnRange.trans eŒ®
    rw [‚Üê x.toGHSpace_rep, ‚Üê y.toGHSpace_rep, toGHSpace_eq_toGHSpace_iff_isometryEquiv]
    exact ‚ü®e‚ü©
  dist_triangle x y z := by
    /- To show the triangular inequality between `X`, `Y` and `Z`,
        realize an optimal coupling between `X` and `Y` in a space `Œ≥1`,
        and an optimal coupling between `Y` and `Z` in a space `Œ≥2`.
        Then, glue these metric spaces along `Y`. We get a new space `Œ≥`
        in which `X` and `Y` are optimally coupled, as well as `Y` and `Z`.
        Apply the triangle inequality for the Hausdorff distance in `Œ≥`
        to conclude. -/
    let X := x.Rep
    let Y := y.Rep
    let Z := z.Rep
    let Œ≥1 := OptimalGHCoupling X Y
    let Œ≥2 := OptimalGHCoupling Y Z
    let Œ¶ : Y ‚Üí Œ≥1 := optimalGHInjr X Y
    have hŒ¶ : Isometry Œ¶ := isometry_optimalGHInjr X Y
    let Œ® : Y ‚Üí Œ≥2 := optimalGHInjl Y Z
    have hŒ® : Isometry Œ® := isometry_optimalGHInjl Y Z
    have Comm : toGlueL hŒ¶ hŒ® ‚àò optimalGHInjr X Y = toGlueR hŒ¶ hŒ® ‚àò optimalGHInjl Y Z :=
      toGlue_commute hŒ¶ hŒ®
    calc
      dist x z = dist (toGHSpace X) (toGHSpace Z) := by
        rw [x.toGHSpace_rep, z.toGHSpace_rep]
      _ ‚â§ hausdorffDist (range (toGlueL hŒ¶ hŒ® ‚àò optimalGHInjl X Y))
            (range (toGlueR hŒ¶ hŒ® ‚àò optimalGHInjr Y Z)) :=
        (ghDist_le_hausdorffDist ((toGlueL_isometry hŒ¶ hŒ®).comp (isometry_optimalGHInjl X Y))
          ((toGlueR_isometry hŒ¶ hŒ®).comp (isometry_optimalGHInjr Y Z)))
      _ ‚â§ hausdorffDist (range (toGlueL hŒ¶ hŒ® ‚àò optimalGHInjl X Y))
              (range (toGlueL hŒ¶ hŒ® ‚àò optimalGHInjr X Y)) +
            hausdorffDist (range (toGlueL hŒ¶ hŒ® ‚àò optimalGHInjr X Y))
              (range (toGlueR hŒ¶ hŒ® ‚àò optimalGHInjr Y Z)) := by
        refine hausdorffDist_triangle <| hausdorffEdist_ne_top_of_nonempty_of_bounded
          (range_nonempty _) (range_nonempty _) ?_ ?_
        ¬∑ exact (isCompact_range (Isometry.continuous
            ((toGlueL_isometry hŒ¶ hŒ®).comp (isometry_optimalGHInjl X Y)))).isBounded
        ¬∑ exact (isCompact_range (Isometry.continuous
            ((toGlueL_isometry hŒ¶ hŒ®).comp (isometry_optimalGHInjr X Y)))).isBounded
      _ = hausdorffDist (toGlueL hŒ¶ hŒ® '' range (optimalGHInjl X Y))
              (toGlueL hŒ¶ hŒ® '' range (optimalGHInjr X Y)) +
            hausdorffDist (toGlueR hŒ¶ hŒ® '' range (optimalGHInjl Y Z))
              (toGlueR hŒ¶ hŒ® '' range (optimalGHInjr Y Z)) := by
        simp only [‚Üê range_comp, Comm, eq_self_iff_true, add_right_inj]
      _ = hausdorffDist (range (optimalGHInjl X Y)) (range (optimalGHInjr X Y)) +
            hausdorffDist (range (optimalGHInjl Y Z)) (range (optimalGHInjr Y Z)) := by
        rw [hausdorffDist_image (toGlueL_isometry hŒ¶ hŒ®),
          hausdorffDist_image (toGlueR_isometry hŒ¶ hŒ®)]
      _ = dist (toGHSpace X) (toGHSpace Y) + dist (toGHSpace Y) (toGHSpace Z) := by
        rw [hausdorffDist_optimal, hausdorffDist_optimal, ghDist, ghDist]
      _ = dist x y + dist y z := by rw [x.toGHSpace_rep, y.toGHSpace_rep, z.toGHSpace_rep]


end GHSpace --section

end GromovHausdorff

/-- In particular, nonempty compacts of a metric space map to `GHSpace`.
    We register this in the `TopologicalSpace` namespace to take advantage
    of the notation `p.toGHSpace`. -/
def TopologicalSpace.NonemptyCompacts.toGHSpace {X : Type u} [MetricSpace X]
    (p : NonemptyCompacts X) : GromovHausdorff.GHSpace :=
  GromovHausdorff.toGHSpace p

namespace GromovHausdorff

section NonemptyCompacts

variable {X : Type u} [MetricSpace X]

theorem ghDist_le_nonemptyCompacts_dist (p q : NonemptyCompacts X) :
    dist p.toGHSpace q.toGHSpace ‚â§ dist p q := by
  have ha : Isometry ((‚Üë) : p ‚Üí X) := isometry_subtype_coe
  have hb : Isometry ((‚Üë) : q ‚Üí X) := isometry_subtype_coe
  have A : dist p q = hausdorffDist (p : Set X) q := rfl
  have I : ‚Üëp = range ((‚Üë) : p ‚Üí X) := Subtype.range_coe_subtype.symm
  have J : ‚Üëq = range ((‚Üë) : q ‚Üí X) := Subtype.range_coe_subtype.symm
  rw [A, I, J]
  exact ghDist_le_hausdorffDist ha hb

theorem toGHSpace_lipschitz :
    LipschitzWith 1 (NonemptyCompacts.toGHSpace : NonemptyCompacts X ‚Üí GHSpace) :=
  LipschitzWith.mk_one ghDist_le_nonemptyCompacts_dist

theorem toGHSpace_continuous :
    Continuous (NonemptyCompacts.toGHSpace : NonemptyCompacts X ‚Üí GHSpace) :=
  toGHSpace_lipschitz.continuous

end NonemptyCompacts

section

/- In this section, we show that if two metric spaces are isometric up to `Œµ‚ÇÇ`, then their
Gromov-Hausdorff distance is bounded by `Œµ‚ÇÇ / 2`. More generally, if there are subsets which are
`Œµ‚ÇÅ`-dense and `Œµ‚ÇÉ`-dense in two spaces, and isometric up to `Œµ‚ÇÇ`, then the Gromov-Hausdorff
distance between the spaces is bounded by `Œµ‚ÇÅ + Œµ‚ÇÇ/2 + Œµ‚ÇÉ`. For this, we construct a suitable
coupling between the two spaces, by gluing them (approximately) along the two matching subsets. -/
variable {X : Type u} [MetricSpace X] [CompactSpace X] [Nonempty X] {Y : Type v} [MetricSpace Y]
  [CompactSpace Y] [Nonempty Y]

/-- If there are subsets which are `Œµ‚ÇÅ`-dense and `Œµ‚ÇÉ`-dense in two spaces, and
isometric up to `Œµ‚ÇÇ`, then the Gromov-Hausdorff distance between the spaces is bounded by
`Œµ‚ÇÅ + Œµ‚ÇÇ/2 + Œµ‚ÇÉ`. -/
theorem ghDist_le_of_approx_subsets {s : Set X} (Œ¶ : s ‚Üí Y) {Œµ‚ÇÅ Œµ‚ÇÇ Œµ‚ÇÉ : ‚Ñù}
    (hs : ‚àÄ x : X, ‚àÉ y ‚àà s, dist x y ‚â§ Œµ‚ÇÅ) (hs' : ‚àÄ x : Y, ‚àÉ y : s, dist x (Œ¶ y) ‚â§ Œµ‚ÇÉ)
    (H : ‚àÄ x y : s, |dist x y - dist (Œ¶ x) (Œ¶ y)| ‚â§ Œµ‚ÇÇ) : ghDist X Y ‚â§ Œµ‚ÇÅ + Œµ‚ÇÇ / 2 + Œµ‚ÇÉ := by
  refine le_of_forall_pos_le_add fun Œ¥ Œ¥0 => ?_
  rcases exists_mem_of_nonempty X with ‚ü®xX, _‚ü©
  rcases hs xX with ‚ü®xs, hxs, Dxs‚ü©
  have sne : s.Nonempty := ‚ü®xs, hxs‚ü©
  let _ : Nonempty s := sne.to_subtype
  have : 0 ‚â§ Œµ‚ÇÇ := le_trans (abs_nonneg _) (H ‚ü®xs, hxs‚ü© ‚ü®xs, hxs‚ü©)
  have : ‚àÄ p q : s, |dist p q - dist (Œ¶ p) (Œ¶ q)| ‚â§ 2 * (Œµ‚ÇÇ / 2 + Œ¥) := fun p q =>
    calc
      |dist p q - dist (Œ¶ p) (Œ¶ q)| ‚â§ Œµ‚ÇÇ := H p q
      _ ‚â§ 2 * (Œµ‚ÇÇ / 2 + Œ¥) := by linarith
  -- glue `X` and `Y` along the almost matching subsets
  let _ : MetricSpace (X ‚äï Y) :=
    glueMetricApprox (fun x : s => (x : X)) (fun x => Œ¶ x) (Œµ‚ÇÇ / 2 + Œ¥) (by linarith) this
  let Fl := @Sum.inl X Y
  let Fr := @Sum.inr X Y
  have Il : Isometry Fl := Isometry.of_dist_eq fun x y => rfl
  have Ir : Isometry Fr := Isometry.of_dist_eq fun x y => rfl
  /- The proof goes as follows : the `ghDist` is bounded by the Hausdorff distance of the images
    in the coupling, which is bounded (using the triangular inequality) by the sum of the Hausdorff
    distances of `X` and `s` (in the coupling or, equivalently in the original space), of `s` and
    `Œ¶ s`, and of `Œ¶ s` and `Y` (in the coupling or, equivalently, in the original space).
    The first term is bounded by `Œµ‚ÇÅ`, by `Œµ‚ÇÅ`-density. The third one is bounded by `Œµ‚ÇÉ`.
    And the middle one is bounded by `Œµ‚ÇÇ/2` as in the coupling the points `x` and `Œ¶ x` are
    at distance `Œµ‚ÇÇ/2` by construction of the coupling (in fact `Œµ‚ÇÇ/2 + Œ¥` where `Œ¥` is an
    arbitrarily small positive constant where positivity is used to ensure that the coupling
    is really a metric space and not a premetric space on `X ‚äï Y`). -/
  have : ghDist X Y ‚â§ hausdorffDist (range Fl) (range Fr) := ghDist_le_hausdorffDist Il Ir
  have :
    hausdorffDist (range Fl) (range Fr) ‚â§
      hausdorffDist (range Fl) (Fl '' s) + hausdorffDist (Fl '' s) (range Fr) :=
    have B : IsBounded (range Fl) := (isCompact_range Il.continuous).isBounded
    hausdorffDist_triangle
      (hausdorffEdist_ne_top_of_nonempty_of_bounded (range_nonempty _) (sne.image _) B
        (B.subset (image_subset_range _ _)))
  have :
    hausdorffDist (Fl '' s) (range Fr) ‚â§
      hausdorffDist (Fl '' s) (Fr '' range Œ¶) + hausdorffDist (Fr '' range Œ¶) (range Fr) :=
    have B : IsBounded (range Fr) := (isCompact_range Ir.continuous).isBounded
    hausdorffDist_triangle'
      (hausdorffEdist_ne_top_of_nonempty_of_bounded ((range_nonempty _).image _) (range_nonempty _)
        (B.subset (image_subset_range _ _)) B)
  have : hausdorffDist (range Fl) (Fl '' s) ‚â§ Œµ‚ÇÅ := by
    rw [‚Üê image_univ, hausdorffDist_image Il]
    have : 0 ‚â§ Œµ‚ÇÅ := le_trans dist_nonneg Dxs
    refine hausdorffDist_le_of_mem_dist this (fun x _ => hs x) fun x _ =>
      ‚ü®x, mem_univ _, by simpa only [dist_self]‚ü©
  have : hausdorffDist (Fl '' s) (Fr '' range Œ¶) ‚â§ Œµ‚ÇÇ / 2 + Œ¥ := by
    refine hausdorffDist_le_of_mem_dist (by linarith) ?_ ?_
    ¬∑ intro x' hx'
      rcases (Set.mem_image _ _ _).1 hx' with ‚ü®x, ‚ü®x_in_s, xx'‚ü©‚ü©
      rw [‚Üê xx']
      use Fr (Œ¶ ‚ü®x, x_in_s‚ü©), mem_image_of_mem Fr (mem_range_self _)
      exact le_of_eq (glueDist_glued_points (fun x : s => (x : X)) Œ¶ (Œµ‚ÇÇ / 2 + Œ¥) ‚ü®x, x_in_s‚ü©)
    ¬∑ intro x' hx'
      rcases (Set.mem_image _ _ _).1 hx' with ‚ü®y, ‚ü®y_in_s', yx'‚ü©‚ü©
      rcases mem_range.1 y_in_s' with ‚ü®x, xy‚ü©
      use Fl x, mem_image_of_mem _ x.2
      rw [‚Üê yx', ‚Üê xy, dist_comm]
      exact le_of_eq (glueDist_glued_points (Z := s) (@Subtype.val X s) Œ¶ (Œµ‚ÇÇ / 2 + Œ¥) x)
  have : hausdorffDist (Fr '' range Œ¶) (range Fr) ‚â§ Œµ‚ÇÉ := by
    rw [‚Üê @image_univ _ _ Fr, hausdorffDist_image Ir]
    rcases exists_mem_of_nonempty Y with ‚ü®xY, _‚ü©
    rcases hs' xY with ‚ü®xs', Dxs'‚ü©
    have : 0 ‚â§ Œµ‚ÇÉ := le_trans dist_nonneg Dxs'
    refine hausdorffDist_le_of_mem_dist this
      (fun x _ => ‚ü®x, mem_univ _, by simpa only [dist_self]‚ü©)
      fun x _ => ?_
    rcases hs' x with ‚ü®y, Dy‚ü©
    exact ‚ü®Œ¶ y, mem_range_self _, Dy‚ü©
  linarith

end

--section
/-- The Gromov-Hausdorff space is second countable. -/
instance : SecondCountableTopology GHSpace := by
  refine secondCountable_of_countable_discretization fun Œ¥ Œ¥pos => ?_
  let Œµ := 2 / 5 * Œ¥
  have Œµpos : 0 < Œµ := mul_pos (by norm_num) Œ¥pos
  have : ‚àÄ p : GHSpace, ‚àÉ s : Set p.Rep, s.Finite ‚àß univ ‚äÜ ‚ãÉ x ‚àà s, ball x Œµ := fun p => by
    simpa only [subset_univ, true_and] using
      finite_cover_balls_of_compact (X := p.Rep) isCompact_univ Œµpos
  -- for each `p`, `s p` is a finite `Œµ`-dense subset of `p` (or rather the metric space
  -- `p.rep` representing `p`)
  choose s hs using this
  have : ‚àÄ p : GHSpace, ‚àÄ t : Set p.Rep, t.Finite ‚Üí ‚àÉ n : ‚Ñï, ‚àÉ _ : Equiv t (Fin n), True := by
    intro p t ht
    let _ : Fintype t := Finite.fintype ht
    exact ‚ü®Fintype.card t, Fintype.equivFin t, trivial‚ü©
  choose N e _ using this
  -- cardinality of the nice finite subset `s p` of `p.rep`, called `N p`
  let N := fun p : GHSpace => N p (s p) (hs p).1
  -- equiv from `s p`, a nice finite subset of `p.rep`, to `Fin (N p)`, called `E p`
  let E := fun p : GHSpace => e p (s p) (hs p).1
  -- A function `F` associating to `p : GHSpace` the data of all distances between points
  -- in the `Œµ`-dense set `s p`.
  let F : GHSpace ‚Üí Œ£ n : ‚Ñï, Fin n ‚Üí Fin n ‚Üí ‚Ñ§ := fun p =>
    ‚ü®N p, fun a b => ‚åäŒµ‚Åª¬π * dist ((E p).symm a) ((E p).symm b)‚åã‚ü©
  refine ‚ü®Œ£ n, Fin n ‚Üí Fin n ‚Üí ‚Ñ§, by infer_instance, F, fun p q hpq => ?_‚ü©
  /- As the target space of F is countable, it suffices to show that two points
    `p` and `q` with `F p = F q` are at distance `‚â§ Œ¥`.
    For this, we construct a map `Œ¶` from `s p ‚äÜ p.rep` (representing `p`)
    to `q.rep` (representing `q`) which is almost an isometry on `s p`, and
    with image `s q`. For this, we compose the identification of `s p` with `Fin (N p)`
    and the inverse of the identification of `s q` with `Fin (N q)`. Together with
    the fact that `N p = N q`, this constructs `Œ®` between `s p` and `s q`, and then
    composing with the canonical inclusion we get `Œ¶`. -/
  have Npq : N p = N q := (Sigma.mk.inj_iff.1 hpq).1
  let Œ® : s p ‚Üí s q := fun x => (E q).symm (Fin.cast Npq ((E p) x))
  let Œ¶ : s p ‚Üí q.Rep := fun x => Œ® x
  -- Use the almost isometry `Œ¶` to show that `p.rep` and `q.rep`
  -- are within controlled Gromov-Hausdorff distance.
  have main : ghDist p.Rep q.Rep ‚â§ Œµ + Œµ / 2 + Œµ := by
    refine ghDist_le_of_approx_subsets Œ¶ ?_ ?_ ?_
    ¬∑ show ‚àÄ x : p.Rep, ‚àÉ y ‚àà s p, dist x y ‚â§ Œµ
      -- by construction, `s p` is `Œµ`-dense
      intro x
      have : x ‚àà ‚ãÉ y ‚àà s p, ball y Œµ := (hs p).2 (mem_univ _)
      rcases mem_iUnion‚ÇÇ.1 this with ‚ü®y, ys, hy‚ü©
      exact ‚ü®y, ys, le_of_lt hy‚ü©
    ¬∑ show ‚àÄ x : q.Rep, ‚àÉ z : s p, dist x (Œ¶ z) ‚â§ Œµ
      -- by construction, `s q` is `Œµ`-dense, and it is the range of `Œ¶`
      intro x
      have : x ‚àà ‚ãÉ y ‚àà s q, ball y Œµ := (hs q).2 (mem_univ _)
      rcases mem_iUnion‚ÇÇ.1 this with ‚ü®y, ys, hy‚ü©
      let i : ‚Ñï := E q ‚ü®y, ys‚ü©
      let hi := ((E q) ‚ü®y, ys‚ü©).is_lt
      have ihi_eq : (‚ü®i, hi‚ü© : Fin (N q)) = (E q) ‚ü®y, ys‚ü© := by rw [Fin.ext_iff, Fin.val_mk]
      have hiq : i < N q := hi
      have hip : i < N p := by rwa [Npq.symm] at hiq
      let z := (E p).symm ‚ü®i, hip‚ü©
      use z
      have C1 : (E p) z = ‚ü®i, hip‚ü© := (E p).apply_symm_apply ‚ü®i, hip‚ü©
      have C2 : Fin.cast Npq ‚ü®i, hip‚ü© = ‚ü®i, hi‚ü© := rfl
      have C3 : (E q).symm ‚ü®i, hi‚ü© = ‚ü®y, ys‚ü© := by
        rw [ihi_eq]; exact (E q).symm_apply_apply ‚ü®y, ys‚ü©
      have : Œ¶ z = y := by simp only [Œ¶, Œ®]; rw [C1, C2, C3]
      rw [this]
      exact le_of_lt hy
    ¬∑ show ‚àÄ x y : s p, |dist x y - dist (Œ¶ x) (Œ¶ y)| ‚â§ Œµ
      /- the distance between `x` and `y` is encoded in `F p`, and the distance between
            `Œ¶ x` and `Œ¶ y` (two points of `s q`) is encoded in `F q`, all this up to `Œµ`.
            As `F p = F q`, the distances are almost equal. -/
      -- Porting note: we have to circumvent the absence of `change ‚Ä¶ with ‚Ä¶ `
      intro x y
      -- have : dist (Œ¶ x) (Œ¶ y) = dist (Œ® x) (Œ® y) := rfl
      rw [show dist (Œ¶ x) (Œ¶ y) = dist (Œ® x) (Œ® y) from rfl]
      -- introduce `i`, that codes both `x` and `Œ¶ x` in `Fin (N p) = Fin (N q)`
      let i : ‚Ñï := E p x
      have hip : i < N p := ((E p) x).2
      have hiq : i < N q := by rwa [Npq] at hip
      have i' : i = (E q) (Œ® x) := by simp only [i, Œ®, Equiv.apply_symm_apply, Fin.coe_cast]
      -- introduce `j`, that codes both `y` and `Œ¶ y` in `Fin (N p) = Fin (N q)`
      let j : ‚Ñï := E p y
      have hjp : j < N p := ((E p) y).2
      have hjq : j < N q := by rwa [Npq] at hjp
      have j' : j = ((E q) (Œ® y)).1 := by
        simp only [j, Œ®, Equiv.apply_symm_apply, Fin.coe_cast]
      -- Express `dist x y` in terms of `F p`
      have : (F p).2 ((E p) x) ((E p) y) = ‚åäŒµ‚Åª¬π * dist x y‚åã := by
        simp only [F, (E p).symm_apply_apply]
      have Ap : (F p).2 ‚ü®i, hip‚ü© ‚ü®j, hjp‚ü© = ‚åäŒµ‚Åª¬π * dist x y‚åã := by rw [‚Üê this]
      -- Express `dist (Œ¶ x) (Œ¶ y)` in terms of `F q`
      have : (F q).2 ((E q) (Œ® x)) ((E q) (Œ® y)) = ‚åäŒµ‚Åª¬π * dist (Œ® x) (Œ® y)‚åã := by
        simp only [F, (E q).symm_apply_apply]
      have Aq : (F q).2 ‚ü®i, hiq‚ü© ‚ü®j, hjq‚ü© = ‚åäŒµ‚Åª¬π * dist (Œ® x) (Œ® y)‚åã := by
        rw [‚Üê this]
        -- Porting note: `congr` fails to make progress
        refine congr_arg‚ÇÇ (F q).2 ?_ ?_ <;> ext1
        exacts [i', j']
      -- use the equality between `F p` and `F q` to deduce that the distances have equal
      -- integer parts
      have : (F p).2 ‚ü®i, hip‚ü© ‚ü®j, hjp‚ü© = (F q).2 ‚ü®i, hiq‚ü© ‚ü®j, hjq‚ü© := by
        have hpq' : HEq (F p).snd (F q).snd := (Sigma.mk.inj_iff.1 hpq).2
        rw [Fin.heq_fun‚ÇÇ_iff Npq Npq] at hpq'
        rw [‚Üê hpq']
      rw [Ap, Aq] at this
      -- deduce that the distances coincide up to `Œµ`, by a straightforward computation
      -- that should be automated
      have I :=
        calc
          |Œµ‚Åª¬π| * |dist x y - dist (Œ® x) (Œ® y)| = |Œµ‚Åª¬π * (dist x y - dist (Œ® x) (Œ® y))| :=
            (abs_mul _ _).symm
          _ = |Œµ‚Åª¬π * dist x y - Œµ‚Åª¬π * dist (Œ® x) (Œ® y)| := by congr; ring
          _ ‚â§ 1 := le_of_lt (abs_sub_lt_one_of_floor_eq_floor this)
      calc
        |dist x y - dist (Œ® x) (Œ® y)| = Œµ * Œµ‚Åª¬π * |dist x y - dist (Œ® x) (Œ® y)| := by
          rw [mul_inv_cancel‚ÇÄ (ne_of_gt Œµpos), one_mul]
        _ = Œµ * (|Œµ‚Åª¬π| * |dist x y - dist (Œ® x) (Œ® y)|) := by
          rw [abs_of_nonneg (le_of_lt (inv_pos.2 Œµpos)), mul_assoc]
        _ ‚â§ Œµ * 1 := mul_le_mul_of_nonneg_left I (le_of_lt Œµpos)
        _ = Œµ := mul_one _
  calc
    dist p q = ghDist p.Rep q.Rep := dist_ghDist p q
    _ ‚â§ Œµ + Œµ / 2 + Œµ := main
    _ = Œ¥ := by ring

/-- Compactness criterion: a closed set of compact metric spaces is compact if the spaces have
a uniformly bounded diameter, and for all `Œµ` the number of balls of radius `Œµ` required
to cover the spaces is uniformly bounded. This is an equivalence, but we only prove the
interesting direction that these conditions imply compactness. -/
theorem totallyBounded {t : Set GHSpace} {C : ‚Ñù} {u : ‚Ñï ‚Üí ‚Ñù} {K : ‚Ñï ‚Üí ‚Ñï}
    (ulim : Tendsto u atTop (ùìù 0)) (hdiam : ‚àÄ p ‚àà t, diam (univ : Set (GHSpace.Rep p)) ‚â§ C)
    (hcov : ‚àÄ p ‚àà t, ‚àÄ n : ‚Ñï, ‚àÉ s : Set (GHSpace.Rep p),
      (#s) ‚â§ K n ‚àß univ ‚äÜ ‚ãÉ x ‚àà s, ball x (u n)) :
    TotallyBounded t := by
  /- Let `Œ¥>0`, and `Œµ = Œ¥/5`. For each `p`, we construct a finite subset `s p` of `p`, which
    is `Œµ`-dense and has cardinality at most `K n`. Encoding the mutual distances of points
    in `s p`, up to `Œµ`, we will get a map `F` associating to `p` finitely many data, and making
    it possible to reconstruct `p` up to `Œµ`. This is enough to prove total boundedness. -/
  refine Metric.totallyBounded_of_finite_discretization fun Œ¥ Œ¥pos => ?_
  let Œµ := 1 / 5 * Œ¥
  have Œµpos : 0 < Œµ := mul_pos (by norm_num) Œ¥pos
  -- choose `n` for which `u n < Œµ`
  rcases Metric.tendsto_atTop.1 ulim Œµ Œµpos with ‚ü®n, hn‚ü©
  have u_le_Œµ : u n ‚â§ Œµ := by
    have := hn n le_rfl
    simp only [Real.dist_eq, add_zero, sub_eq_add_neg, neg_zero] at this
    exact le_of_lt (lt_of_le_of_lt (le_abs_self _) this)
  -- construct a finite subset `s p` of `p` which is `Œµ`-dense and has cardinal `‚â§ K n`
  have :
    ‚àÄ p : GHSpace,
      ‚àÉ s : Set p.Rep, ‚àÉ N ‚â§ K n, ‚àÉ _ : Equiv s (Fin N), p ‚àà t ‚Üí univ ‚äÜ ‚ãÉ x ‚àà s, ball x (u n) := by
    intro p
    by_cases hp : p ‚àâ t
    ¬∑ have : Nonempty (Equiv (‚àÖ : Set p.Rep) (Fin 0)) := by
        rw [‚Üê Fintype.card_eq, card_empty, Fintype.card_fin]
      use ‚àÖ, 0, bot_le, this.some
      exact fun hp' => (hp hp').elim
    ¬∑ rcases hcov _ (Set.not_notMem.1 hp) n with ‚ü®s, ‚ü®scard, scover‚ü©‚ü©
      rcases Cardinal.lt_aleph0.1 (lt_of_le_of_lt scard (Cardinal.nat_lt_aleph0 _)) with ‚ü®N, hN‚ü©
      rw [hN, Nat.cast_le] at scard
      have : #s = #(Fin N) := by rw [hN, Cardinal.mk_fin]
      obtain ‚ü®E‚ü© := Quotient.exact this
      use s, N, scard, E
      simp only [scover, imp_true_iff]
  choose s N hN E hs using this
  -- Define a function `F` taking values in a finite type and associating to `p` enough data
  -- to reconstruct it up to `Œµ`, namely the (discretized) distances between elements of `s p`.
  let M := ‚åäŒµ‚Åª¬π * max C 0‚åã‚Çä
  let F : GHSpace ‚Üí Œ£ k : Fin (K n).succ, Fin k ‚Üí Fin k ‚Üí Fin M.succ := fun p =>
    ‚ü®‚ü®N p, lt_of_le_of_lt (hN p) (Nat.lt_succ_self _)‚ü©, fun a b =>
      ‚ü®min M ‚åäŒµ‚Åª¬π * dist ((E p).symm a) ((E p).symm b)‚åã‚Çä,
        (min_le_left _ _).trans_lt (Nat.lt_succ_self _)‚ü©‚ü©
  refine ‚ü®_, ?_, fun p => F p, ?_‚ü©
  ¬∑ infer_instance
  -- It remains to show that if `F p = F q`, then `p` and `q` are `Œµ`-close
  rintro ‚ü®p, pt‚ü© ‚ü®q, qt‚ü© hpq
  have Npq : N p = N q := Fin.ext_iff.1 (Sigma.mk.inj_iff.1 hpq).1
  let Œ® : s p ‚Üí s q := fun x => (E q).symm (Fin.cast Npq ((E p) x))
  let Œ¶ : s p ‚Üí q.Rep := fun x => Œ® x
  have main : ghDist p.Rep q.Rep ‚â§ Œµ + Œµ / 2 + Œµ := by
    -- to prove the main inequality, argue that `s p` is `Œµ`-dense in `p`, and `s q` is `Œµ`-dense
    -- in `q`, and `s p` and `s q` are almost isometric. Then closeness follows
    -- from `ghDist_le_of_approx_subsets`
    refine ghDist_le_of_approx_subsets Œ¶ ?_ ?_ ?_
    ¬∑ show ‚àÄ x : p.Rep, ‚àÉ y ‚àà s p, dist x y ‚â§ Œµ
      -- by construction, `s p` is `Œµ`-dense
      intro x
      have : x ‚àà ‚ãÉ y ‚àà s p, ball y (u n) := (hs p pt) (mem_univ _)
      rcases mem_iUnion‚ÇÇ.1 this with ‚ü®y, ys, hy‚ü©
      exact ‚ü®y, ys, le_trans (le_of_lt hy) u_le_Œµ‚ü©
    ¬∑ show ‚àÄ x : q.Rep, ‚àÉ z : s p, dist x (Œ¶ z) ‚â§ Œµ
      -- by construction, `s q` is `Œµ`-dense, and it is the range of `Œ¶`
      intro x
      have : x ‚àà ‚ãÉ y ‚àà s q, ball y (u n) := (hs q qt) (mem_univ _)
      rcases mem_iUnion‚ÇÇ.1 this with ‚ü®y, ys, hy‚ü©
      let i : ‚Ñï := E q ‚ü®y, ys‚ü©
      let hi := ((E q) ‚ü®y, ys‚ü©).2
      have ihi_eq : (‚ü®i, hi‚ü© : Fin (N q)) = (E q) ‚ü®y, ys‚ü© := by rw [Fin.ext_iff, Fin.val_mk]
      have hiq : i < N q := hi
      have hip : i < N p := by rwa [Npq.symm] at hiq
      let z := (E p).symm ‚ü®i, hip‚ü©
      use z
      have C1 : (E p) z = ‚ü®i, hip‚ü© := (E p).apply_symm_apply ‚ü®i, hip‚ü©
      have C2 : Fin.cast Npq ‚ü®i, hip‚ü© = ‚ü®i, hi‚ü© := rfl
      have C3 : (E q).symm ‚ü®i, hi‚ü© = ‚ü®y, ys‚ü© := by
        rw [ihi_eq]; exact (E q).symm_apply_apply ‚ü®y, ys‚ü©
      have : Œ¶ z = y := by simp only [Œ®, Œ¶]; rw [C1, C2, C3]
      rw [this]
      exact le_trans (le_of_lt hy) u_le_Œµ
    ¬∑ show ‚àÄ x y : s p, |dist x y - dist (Œ¶ x) (Œ¶ y)| ‚â§ Œµ
      /- the distance between `x` and `y` is encoded in `F p`, and the distance between
            `Œ¶ x` and `Œ¶ y` (two points of `s q`) is encoded in `F q`, all this up to `Œµ`.
            As `F p = F q`, the distances are almost equal. -/
      intro x y
      have : dist (Œ¶ x) (Œ¶ y) = dist (Œ® x) (Œ® y) := rfl
      rw [this]
      -- introduce `i`, that codes both `x` and `Œ¶ x` in `Fin (N p) = Fin (N q)`
      let i : ‚Ñï := E p x
      have hip : i < N p := ((E p) x).2
      have hiq : i < N q := by rwa [Npq] at hip
      have i' : i = (E q) (Œ® x) := by simp only [i, Œ®, Equiv.apply_symm_apply, Fin.coe_cast]
      -- introduce `j`, that codes both `y` and `Œ¶ y` in `Fin (N p) = Fin (N q)`
      let j : ‚Ñï := E p y
      have hjp : j < N p := ((E p) y).2
      have hjq : j < N q := by rwa [Npq] at hjp
      have j' : j = (E q) (Œ® y) := by simp only [j, Œ®, Equiv.apply_symm_apply, Fin.coe_cast]
      -- Express `dist x y` in terms of `F p`
      have Ap : ((F p).2 ‚ü®i, hip‚ü© ‚ü®j, hjp‚ü©).1 = ‚åäŒµ‚Åª¬π * dist x y‚åã‚Çä :=
        calc
          ((F p).2 ‚ü®i, hip‚ü© ‚ü®j, hjp‚ü©).1 = ((F p).2 ((E p) x) ((E p) y)).1 := by
            congr
          _ = min M ‚åäŒµ‚Åª¬π * dist x y‚åã‚Çä := by simp only [F, (E p).symm_apply_apply]
          _ = ‚åäŒµ‚Åª¬π * dist x y‚åã‚Çä := by
            refine min_eq_right (Nat.floor_mono ?_)
            refine mul_le_mul_of_nonneg_left (le_trans ?_ (le_max_left _ _)) (inv_pos.2 Œµpos).le
            change dist (x : p.Rep) y ‚â§ C
            refine (dist_le_diam_of_mem isCompact_univ.isBounded (mem_univ _) (mem_univ _)).trans ?_
            exact hdiam p pt
      -- Express `dist (Œ¶ x) (Œ¶ y)` in terms of `F q`
      have Aq : ((F q).2 ‚ü®i, hiq‚ü© ‚ü®j, hjq‚ü©).1 = ‚åäŒµ‚Åª¬π * dist (Œ® x) (Œ® y)‚åã‚Çä :=
        calc
          ((F q).2 ‚ü®i, hiq‚ü© ‚ü®j, hjq‚ü©).1 = ((F q).2 ((E q) (Œ® x)) ((E q) (Œ® y))).1 := by
            -- Porting note: `congr` drops `Fin.val` but fails to make further progress
            exact congr_arg‚ÇÇ (Fin.val <| (F q).2 ¬∑ ¬∑) (Fin.ext i') (Fin.ext j')
          _ = min M ‚åäŒµ‚Åª¬π * dist (Œ® x) (Œ® y)‚åã‚Çä := by simp only [F, (E q).symm_apply_apply]
          _ = ‚åäŒµ‚Åª¬π * dist (Œ® x) (Œ® y)‚åã‚Çä := by
            refine min_eq_right (Nat.floor_mono ?_)
            refine mul_le_mul_of_nonneg_left (le_trans ?_ (le_max_left _ _)) (inv_pos.2 Œµpos).le
            change dist (Œ® x : q.Rep) (Œ® y) ‚â§ C
            refine (dist_le_diam_of_mem isCompact_univ.isBounded (mem_univ _) (mem_univ _)).trans ?_
            exact hdiam q qt
      -- use the equality between `F p` and `F q` to deduce that the distances have equal
      -- integer parts
      have : ((F p).2 ‚ü®i, hip‚ü© ‚ü®j, hjp‚ü©).1 = ((F q).2 ‚ü®i, hiq‚ü© ‚ü®j, hjq‚ü©).1 := by
        have hpq' : HEq (F p).snd (F q).snd := (Sigma.mk.inj_iff.1 hpq).2
        rw [Fin.heq_fun‚ÇÇ_iff Npq Npq] at hpq'
        rw [‚Üê hpq']
      have : ‚åäŒµ‚Åª¬π * dist x y‚åã = ‚åäŒµ‚Åª¬π * dist (Œ® x) (Œ® y)‚åã := by
        rw [Ap, Aq] at this
        have D : 0 ‚â§ ‚åäŒµ‚Åª¬π * dist x y‚åã :=
          floor_nonneg.2 (mul_nonneg (le_of_lt (inv_pos.2 Œµpos)) dist_nonneg)
        have D' : 0 ‚â§ ‚åäŒµ‚Åª¬π * dist (Œ® x) (Œ® y)‚åã :=
          floor_nonneg.2 (mul_nonneg (le_of_lt (inv_pos.2 Œµpos)) dist_nonneg)
        rw [‚Üê Int.toNat_of_nonneg D, ‚Üê Int.toNat_of_nonneg D', Int.floor_toNat, Int.floor_toNat,
          this]
      -- deduce that the distances coincide up to `Œµ`, by a straightforward computation
      -- that should be automated
      have I :=
        calc
          |Œµ‚Åª¬π| * |dist x y - dist (Œ® x) (Œ® y)| = |Œµ‚Åª¬π * (dist x y - dist (Œ® x) (Œ® y))| :=
            (abs_mul _ _).symm
          _ = |Œµ‚Åª¬π * dist x y - Œµ‚Åª¬π * dist (Œ® x) (Œ® y)| := by congr; ring
          _ ‚â§ 1 := le_of_lt (abs_sub_lt_one_of_floor_eq_floor this)
      calc
        |dist x y - dist (Œ® x) (Œ® y)| = Œµ * Œµ‚Åª¬π * |dist x y - dist (Œ® x) (Œ® y)| := by
          rw [mul_inv_cancel‚ÇÄ (ne_of_gt Œµpos), one_mul]
        _ = Œµ * (|Œµ‚Åª¬π| * |dist x y - dist (Œ® x) (Œ® y)|) := by
          rw [abs_of_nonneg (le_of_lt (inv_pos.2 Œµpos)), mul_assoc]
        _ ‚â§ Œµ * 1 := mul_le_mul_of_nonneg_left I (le_of_lt Œµpos)
        _ = Œµ := mul_one _
  calc
    dist p q = ghDist p.Rep q.Rep := dist_ghDist p q
    _ ‚â§ Œµ + Œµ / 2 + Œµ := main
    _ = Œ¥ / 2 := by simp only [Œµ, one_div]; ring
    _ < Œ¥ := half_lt_self Œ¥pos

section Complete

/- We will show that a sequence `u n` of compact metric spaces satisfying
`dist (u n) (u (n+1)) < 1/2^n` converges, which implies completeness of the Gromov-Hausdorff space.
We need to exhibit the limiting compact metric space. For this, start from
a sequence `X n` of representatives of `u n`, and glue in an optimal way `X n` to `X (n+1)`
for all `n`, in a common metric space. Formally, this is done as follows.
Start from `Y 0 = X 0`. Then, glue `X 0` to `X 1` in an optimal way, yielding a space
`Y 1` (with an embedding of `X 1`). Then, consider an optimal gluing of `X 1` and `X 2`, and
glue it to `Y 1` along their common subspace `X 1`. This gives a new space `Y 2`, with an
embedding of `X 2`. Go on, to obtain a sequence of spaces `Y n`. Let `Z0` be the inductive
limit of the `Y n`, and finally let `Z` be the completion of `Z0`.
The images `X2 n` of `X n` in `Z` are at Hausdorff distance `< 1/2^n` by construction, hence they
form a Cauchy sequence for the Hausdorff distance. By completeness (of `Z`, and therefore of its
set of nonempty compact subsets), they converge to a limit `L`. This is the nonempty
compact metric space we are looking for. -/
variable (X : ‚Ñï ‚Üí Type) [‚àÄ n, MetricSpace (X n)] [‚àÄ n, CompactSpace (X n)] [‚àÄ n, Nonempty (X n)]

/-- Auxiliary structure used to glue metric spaces below, recording an isometric embedding
of a type `A` in another metric space. -/
structure AuxGluingStruct (A : Type) [MetricSpace A] : Type 1 where
  Space : Type
  metric : MetricSpace Space
  embed : A ‚Üí Space
  isom : Isometry embed

attribute [local instance] AuxGluingStruct.metric

instance (A : Type) [MetricSpace A] : Inhabited (AuxGluingStruct A) :=
  ‚ü®{  Space := A
      metric := by infer_instance
      embed := id
      isom _ _ := rfl }‚ü©

/-- Auxiliary sequence of metric spaces, containing copies of `X 0`, ..., `X n`, where each
`X i` is glued to `X (i+1)` in an optimal way. The space at step `n+1` is obtained from the space
at step `n` by adding `X (n+1)`, glued in an optimal way to the `X n` already sitting there. -/
def auxGluing (n : ‚Ñï) : AuxGluingStruct (X n) :=
  Nat.recOn n default fun n Y =>
    { Space := GlueSpace Y.isom (isometry_optimalGHInjl (X n) (X (n + 1)))
      metric := by infer_instance
      embed :=
        toGlueR Y.isom (isometry_optimalGHInjl (X n) (X (n + 1))) ‚àò optimalGHInjr (X n) (X (n + 1))
      isom := (toGlueR_isometry _ _).comp (isometry_optimalGHInjr (X n) (X (n + 1))) }

/-- The Gromov-Hausdorff space is complete. -/
instance : CompleteSpace GHSpace := by
  set d := fun n : ‚Ñï ‚Ü¶ ((1 : ‚Ñù) / 2) ^ n
  have : ‚àÄ n : ‚Ñï, 0 < d n := fun _ ‚Ü¶ by positivity
  -- start from a sequence of nonempty compact metric spaces within distance `1/2^n` of each other
  refine Metric.complete_of_convergent_controlled_sequences d this fun u hu => ?_
  -- `X n` is a representative of `u n`
  let X n := (u n).Rep
  -- glue them together successively in an optimal way, getting a sequence of metric spaces `Y n`
  let Y := auxGluing X
  -- this equality is true by definition but Lean unfolds some defs in the wrong order
  have E :
    ‚àÄ n : ‚Ñï,
      GlueSpace (Y n).isom (isometry_optimalGHInjl (X n) (X (n + 1))) = (Y (n + 1)).Space :=
    fun n => by dsimp only [Y, auxGluing]
  let c n := cast (E n)
  have ic : ‚àÄ n, Isometry (c n) := fun n x y => by dsimp only [Y, auxGluing]; exact rfl
  -- there is a canonical embedding of `Y n` in `Y (n+1)`, by construction
  let f : ‚àÄ n, (Y n).Space ‚Üí (Y (n + 1)).Space := fun n =>
    c n ‚àò toGlueL (Y n).isom (isometry_optimalGHInjl (X n) (X n.succ))
  have I : ‚àÄ n, Isometry (f n) := fun n => (ic n).comp (toGlueL_isometry _ _)
  -- consider the inductive limit `Z0` of the `Y n`, and then its completion `Z`
  let Z0 := Metric.InductiveLimit I
  let Z := UniformSpace.Completion Z0
  let Œ¶ := toInductiveLimit I
  let coeZ := ((‚Üë) : Z0 ‚Üí Z)
  -- let `X2 n` be the image of `X n` in the space `Z`
  let X2 n := range (coeZ ‚àò Œ¶ n ‚àò (Y n).embed)
  have isom : ‚àÄ n, Isometry (coeZ ‚àò Œ¶ n ‚àò (Y n).embed) := by
    intro n
    refine UniformSpace.Completion.coe_isometry.comp ?_
    exact (toInductiveLimit_isometry _ _).comp (Y n).isom
  -- The Hausdorff distance of `X2 n` and `X2 (n+1)` is by construction the distance between
  -- `u n` and `u (n+1)`, therefore bounded by `1/2^n`
  have X2n : ‚àÄ n, X2 n =
    range ((coeZ ‚àò Œ¶ n.succ ‚àò c n ‚àò toGlueR (Y n).isom
      (isometry_optimalGHInjl (X n) (X n.succ))) ‚àò optimalGHInjl (X n) (X n.succ)) := by
    intro n
    change X2 n = range (coeZ ‚àò Œ¶ n.succ ‚àò c n ‚àò
      toGlueR (Y n).isom (isometry_optimalGHInjl (X n) (X n.succ)) ‚àò
      optimalGHInjl (X n) (X n.succ))
    simp only [X2, Œ¶]
    rw [‚Üê toInductiveLimit_commute I]
    simp only [f, ‚Üê toGlue_commute, Function.comp_assoc]
  have X2nsucc : ‚àÄ n, X2 n.succ =
      range ((coeZ ‚àò Œ¶ n.succ ‚àò c n ‚àò toGlueR (Y n).isom
        (isometry_optimalGHInjl (X n) (X n.succ))) ‚àò optimalGHInjr (X n) (X n.succ)) := by
    intro n
    rfl
  have D2 : ‚àÄ n, hausdorffDist (X2 n) (X2 n.succ) < d n := fun n ‚Ü¶ by
    rw [X2n n, X2nsucc n, range_comp, range_comp, hausdorffDist_image,
      hausdorffDist_optimal, ‚Üê dist_ghDist]
    ¬∑ exact hu n n n.succ (le_refl n) (le_succ n)
    ¬∑ apply UniformSpace.Completion.coe_isometry.comp _
      exact (toInductiveLimit_isometry _ _).comp ((ic n).comp (toGlueR_isometry _ _))
  -- consider `X2 n` as a member `X3 n` of the type of nonempty compact subsets of `Z`, which
  -- is a metric space
  let X3 : ‚Ñï ‚Üí NonemptyCompacts Z := fun n =>
    ‚ü®‚ü®X2 n, isCompact_range (isom n).continuous‚ü©, range_nonempty _‚ü©
  -- `X3 n` is a Cauchy sequence by construction, as the successive distances are
  -- bounded by `(1/2)^n`
  have : CauchySeq X3 := by
    refine cauchySeq_of_le_geometric (1 / 2) 1 (by norm_num) fun n => ?_
    rw [one_mul]
    exact le_of_lt (D2 n)
  -- therefore, it converges to a limit `L`
  rcases cauchySeq_tendsto_of_complete this with ‚ü®L, hL‚ü©
  -- By construction, the image of `X3 n` in the Gromov-Hausdorff space is `u n`.
  have : ‚àÄ n, (NonemptyCompacts.toGHSpace ‚àò X3) n = u n := by
    intro n
    rw [Function.comp_apply, NonemptyCompacts.toGHSpace, ‚Üê (u n).toGHSpace_rep,
      toGHSpace_eq_toGHSpace_iff_isometryEquiv]
    constructor
    convert (isom n).isometryEquivOnRange.symm
  -- the images of `X3 n` in the Gromov-Hausdorff space converge to the image of `L`
  -- so the images of `u n` converge to the image of `L` as well
  use L.toGHSpace
  apply Filter.Tendsto.congr this
  refine Tendsto.comp ?_ hL
  apply toGHSpace_continuous.tendsto

end Complete --section

end GromovHausdorff --namespace
