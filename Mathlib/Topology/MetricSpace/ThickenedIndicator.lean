/-
Copyright (c) 2022 Kalle Kyt√∂l√§. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle Kyt√∂l√§
-/
import Mathlib.Data.ENNReal.Lemmas
import Mathlib.Topology.MetricSpace.Thickening
import Mathlib.Topology.ContinuousMap.Bounded.Basic

/-!
# Thickened indicators

This file is about thickened indicators of sets in (pseudo e)metric spaces. For a decreasing
sequence of thickening radii tending to 0, the thickened indicators of a closed set form a
decreasing pointwise converging approximation of the indicator function of the set, where the
members of the approximating sequence are nonnegative bounded continuous functions.

## Main definitions

* `thickenedIndicatorAux Œ¥ E`: The `Œ¥`-thickened indicator of a set `E` as an
  unbundled `‚Ñù‚â•0‚àû`-valued function.
* `thickenedIndicator Œ¥ E`: The `Œ¥`-thickened indicator of a set `E` as a bundled
  bounded continuous `‚Ñù‚â•0`-valued function.

## Main results

* For a sequence of thickening radii tending to 0, the `Œ¥`-thickened indicators of a set `E` tend
  pointwise to the indicator of `closure E`.
  - `thickenedIndicatorAux_tendsto_indicator_closure`: The version is for the
    unbundled `‚Ñù‚â•0‚àû`-valued functions.
  - `thickenedIndicator_tendsto_indicator_closure`: The version is for the bundled `‚Ñù‚â•0`-valued
    bounded continuous functions.

-/

open NNReal ENNReal Topology BoundedContinuousFunction Set Metric EMetric Filter

noncomputable section thickenedIndicator

variable {Œ± : Type*} [PseudoEMetricSpace Œ±]

/-- The `Œ¥`-thickened indicator of a set `E` is the function that equals `1` on `E`
and `0` outside a `Œ¥`-thickening of `E` and interpolates (continuously) between
these values using `infEdist _ E`.

`thickenedIndicatorAux` is the unbundled `‚Ñù‚â•0‚àû`-valued function. See `thickenedIndicator`
for the (bundled) bounded continuous function with `‚Ñù‚â•0`-values. -/
def thickenedIndicatorAux (Œ¥ : ‚Ñù) (E : Set Œ±) : Œ± ‚Üí ‚Ñù‚â•0‚àû :=
  fun x : Œ± => (1 : ‚Ñù‚â•0‚àû) - infEdist x E / ENNReal.ofReal Œ¥

theorem continuous_thickenedIndicatorAux {Œ¥ : ‚Ñù} (Œ¥_pos : 0 < Œ¥) (E : Set Œ±) :
    Continuous (thickenedIndicatorAux Œ¥ E) := by
  unfold thickenedIndicatorAux
  let f := fun x : Œ± => (‚ü®1, infEdist x E / ENNReal.ofReal Œ¥‚ü© : ‚Ñù‚â•0 √ó ‚Ñù‚â•0‚àû)
  let sub := fun p : ‚Ñù‚â•0 √ó ‚Ñù‚â•0‚àû => (p.1 : ‚Ñù‚â•0‚àû) - p.2
  rw [show (fun x : Œ± => (1 : ‚Ñù‚â•0‚àû) - infEdist x E / ENNReal.ofReal Œ¥) = sub ‚àò f by rfl]
  apply (@ENNReal.continuous_nnreal_sub 1).comp
  apply (ENNReal.continuous_div_const (ENNReal.ofReal Œ¥) _).comp continuous_infEdist
  norm_num [Œ¥_pos]

theorem thickenedIndicatorAux_le_one (Œ¥ : ‚Ñù) (E : Set Œ±) (x : Œ±) :
    thickenedIndicatorAux Œ¥ E x ‚â§ 1 := by
  apply tsub_le_self (Œ± := ‚Ñù‚â•0‚àû)

theorem thickenedIndicatorAux_lt_top {Œ¥ : ‚Ñù} {E : Set Œ±} {x : Œ±} :
    thickenedIndicatorAux Œ¥ E x < ‚àû :=
  lt_of_le_of_lt (thickenedIndicatorAux_le_one _ _ _) one_lt_top

theorem thickenedIndicatorAux_closure_eq (Œ¥ : ‚Ñù) (E : Set Œ±) :
    thickenedIndicatorAux Œ¥ (closure E) = thickenedIndicatorAux Œ¥ E := by
  simp +unfoldPartialApp only [thickenedIndicatorAux, infEdist_closure]

theorem thickenedIndicatorAux_one (Œ¥ : ‚Ñù) (E : Set Œ±) {x : Œ±} (x_in_E : x ‚àà E) :
    thickenedIndicatorAux Œ¥ E x = 1 := by
  simp [thickenedIndicatorAux, infEdist_zero_of_mem x_in_E, tsub_zero]

theorem thickenedIndicatorAux_one_of_mem_closure (Œ¥ : ‚Ñù) (E : Set Œ±) {x : Œ±}
    (x_mem : x ‚àà closure E) : thickenedIndicatorAux Œ¥ E x = 1 := by
  rw [‚Üê thickenedIndicatorAux_closure_eq, thickenedIndicatorAux_one Œ¥ (closure E) x_mem]

theorem thickenedIndicatorAux_zero {Œ¥ : ‚Ñù} (Œ¥_pos : 0 < Œ¥) (E : Set Œ±) {x : Œ±}
    (x_out : x ‚àâ thickening Œ¥ E) : thickenedIndicatorAux Œ¥ E x = 0 := by
  rw [thickening, mem_setOf_eq, not_lt] at x_out
  unfold thickenedIndicatorAux
  apply le_antisymm _ bot_le
  have key := tsub_le_tsub
    (@rfl _ (1 : ‚Ñù‚â•0‚àû)).le (ENNReal.div_le_div x_out (@rfl _ (ENNReal.ofReal Œ¥ : ‚Ñù‚â•0‚àû)).le)
  rw [ENNReal.div_self (ne_of_gt (ENNReal.ofReal_pos.mpr Œ¥_pos)) ofReal_ne_top] at key
  simpa [tsub_self] using key

theorem thickenedIndicatorAux_mono {Œ¥‚ÇÅ Œ¥‚ÇÇ : ‚Ñù} (hle : Œ¥‚ÇÅ ‚â§ Œ¥‚ÇÇ) (E : Set Œ±) :
    thickenedIndicatorAux Œ¥‚ÇÅ E ‚â§ thickenedIndicatorAux Œ¥‚ÇÇ E :=
  fun _ => tsub_le_tsub (@rfl ‚Ñù‚â•0‚àû 1).le (ENNReal.div_le_div rfl.le (ofReal_le_ofReal hle))

theorem indicator_le_thickenedIndicatorAux (Œ¥ : ‚Ñù) (E : Set Œ±) :
    (E.indicator fun _ => (1 : ‚Ñù‚â•0‚àû)) ‚â§ thickenedIndicatorAux Œ¥ E := by
  intro a
  by_cases h : a ‚àà E
  ¬∑ simp only [h, indicator_of_mem, thickenedIndicatorAux_one Œ¥ E h, le_refl]
  ¬∑ simp only [h, indicator_of_notMem, not_false_iff, zero_le]

theorem thickenedIndicatorAux_subset (Œ¥ : ‚Ñù) {E‚ÇÅ E‚ÇÇ : Set Œ±} (subset : E‚ÇÅ ‚äÜ E‚ÇÇ) :
    thickenedIndicatorAux Œ¥ E‚ÇÅ ‚â§ thickenedIndicatorAux Œ¥ E‚ÇÇ :=
  fun _ => tsub_le_tsub (@rfl ‚Ñù‚â•0‚àû 1).le (ENNReal.div_le_div (infEdist_anti subset) rfl.le)

/-- As the thickening radius Œ¥ tends to 0, the Œ¥-thickened indicator of a set E (in Œ±) tends
pointwise (i.e., w.r.t. the product topology on `Œ± ‚Üí ‚Ñù‚â•0‚àû`) to the indicator function of the
closure of E.

This statement is for the unbundled `‚Ñù‚â•0‚àû`-valued functions `thickenedIndicatorAux Œ¥ E`, see
`thickenedIndicator_tendsto_indicator_closure` for the version for bundled `‚Ñù‚â•0`-valued
bounded continuous functions. -/
theorem thickenedIndicatorAux_tendsto_indicator_closure {Œ¥seq : ‚Ñï ‚Üí ‚Ñù}
    (Œ¥seq_lim : Tendsto Œ¥seq atTop (ùìù 0)) (E : Set Œ±) :
    Tendsto (fun n => thickenedIndicatorAux (Œ¥seq n) E) atTop
      (ùìù (indicator (closure E) fun _ => (1 : ‚Ñù‚â•0‚àû))) := by
  rw [tendsto_pi_nhds]
  intro x
  by_cases x_mem_closure : x ‚àà closure E
  ¬∑ simp_rw [thickenedIndicatorAux_one_of_mem_closure _ E x_mem_closure]
    rw [show (indicator (closure E) fun _ => (1 : ‚Ñù‚â•0‚àû)) x = 1 by
        simp only [x_mem_closure, indicator_of_mem]]
    exact tendsto_const_nhds
  ¬∑ rw [show (closure E).indicator (fun _ => (1 : ‚Ñù‚â•0‚àû)) x = 0 by
        simp only [x_mem_closure, indicator_of_notMem, not_false_iff]]
    rcases exists_real_pos_lt_infEdist_of_notMem_closure x_mem_closure with ‚ü®Œµ, ‚ü®Œµ_pos, Œµ_lt‚ü©‚ü©
    rw [Metric.tendsto_nhds] at Œ¥seq_lim
    specialize Œ¥seq_lim Œµ Œµ_pos
    simp only [dist_zero_right, Real.norm_eq_abs, eventually_atTop] at Œ¥seq_lim
    rcases Œ¥seq_lim with ‚ü®N, hN‚ü©
    apply tendsto_atTop_of_eventually_const (i‚ÇÄ := N)
    intro n n_large
    have key : x ‚àâ thickening Œµ E := by simpa only [thickening, mem_setOf_eq, not_lt] using Œµ_lt.le
    refine le_antisymm ?_ bot_le
    apply (thickenedIndicatorAux_mono (lt_of_abs_lt (hN n n_large)).le E x).trans
    exact (thickenedIndicatorAux_zero Œµ_pos E key).le

/-- The `Œ¥`-thickened indicator of a set `E` is the function that equals `1` on `E`
and `0` outside a `Œ¥`-thickening of `E` and interpolates (continuously) between
these values using `infEdist _ E`.

`thickenedIndicator` is the (bundled) bounded continuous function with `‚Ñù‚â•0`-values.
See `thickenedIndicatorAux` for the unbundled `‚Ñù‚â•0‚àû`-valued function. -/
@[simps]
def thickenedIndicator {Œ¥ : ‚Ñù} (Œ¥_pos : 0 < Œ¥) (E : Set Œ±) : Œ± ‚Üí·µá ‚Ñù‚â•0 where
  toFun := fun x : Œ± => (thickenedIndicatorAux Œ¥ E x).toNNReal
  continuous_toFun := by
    apply ContinuousOn.comp_continuous continuousOn_toNNReal
      (continuous_thickenedIndicatorAux Œ¥_pos E)
    intro x
    exact (lt_of_le_of_lt (@thickenedIndicatorAux_le_one _ _ Œ¥ E x) one_lt_top).ne
  map_bounded' := by
    use 2
    intro x y
    rw [NNReal.dist_eq]
    apply (abs_sub _ _).trans
    rw [NNReal.abs_eq, NNReal.abs_eq, ‚Üê one_add_one_eq_two]
    have key := @thickenedIndicatorAux_le_one _ _ Œ¥ E
    apply add_le_add <;>
      ¬∑ norm_cast
        exact (toNNReal_le_toNNReal (lt_of_le_of_lt (key _) one_lt_top).ne one_ne_top).mpr (key _)

theorem thickenedIndicator.coeFn_eq_comp {Œ¥ : ‚Ñù} (Œ¥_pos : 0 < Œ¥) (E : Set Œ±) :
    ‚áë(thickenedIndicator Œ¥_pos E) = ENNReal.toNNReal ‚àò thickenedIndicatorAux Œ¥ E :=
  rfl

theorem thickenedIndicator_le_one {Œ¥ : ‚Ñù} (Œ¥_pos : 0 < Œ¥) (E : Set Œ±) (x : Œ±) :
    thickenedIndicator Œ¥_pos E x ‚â§ 1 := by
  rw [thickenedIndicator.coeFn_eq_comp]
  simpa using (toNNReal_le_toNNReal thickenedIndicatorAux_lt_top.ne one_ne_top).mpr
    (thickenedIndicatorAux_le_one Œ¥ E x)

theorem thickenedIndicator_one_of_mem_closure {Œ¥ : ‚Ñù} (Œ¥_pos : 0 < Œ¥) (E : Set Œ±) {x : Œ±}
    (x_mem : x ‚àà closure E) : thickenedIndicator Œ¥_pos E x = 1 := by
  rw [thickenedIndicator_apply, thickenedIndicatorAux_one_of_mem_closure Œ¥ E x_mem, toNNReal_one]

lemma one_le_thickenedIndicator_apply' {X : Type _} [PseudoEMetricSpace X]
    {Œ¥ : ‚Ñù} (Œ¥_pos : 0 < Œ¥) {F : Set X} {x : X} (hxF : x ‚àà closure F) :
    1 ‚â§ thickenedIndicator Œ¥_pos F x := by
  rw [thickenedIndicator_one_of_mem_closure Œ¥_pos F hxF]

lemma one_le_thickenedIndicator_apply (X : Type _) [PseudoEMetricSpace X]
    {Œ¥ : ‚Ñù} (Œ¥_pos : 0 < Œ¥) {F : Set X} {x : X} (hxF : x ‚àà F) :
    1 ‚â§ thickenedIndicator Œ¥_pos F x :=
  one_le_thickenedIndicator_apply' Œ¥_pos (subset_closure hxF)

theorem thickenedIndicator_one {Œ¥ : ‚Ñù} (Œ¥_pos : 0 < Œ¥) (E : Set Œ±) {x : Œ±} (x_in_E : x ‚àà E) :
    thickenedIndicator Œ¥_pos E x = 1 :=
  thickenedIndicator_one_of_mem_closure _ _ (subset_closure x_in_E)

theorem thickenedIndicator_zero {Œ¥ : ‚Ñù} (Œ¥_pos : 0 < Œ¥) (E : Set Œ±) {x : Œ±}
    (x_out : x ‚àâ thickening Œ¥ E) : thickenedIndicator Œ¥_pos E x = 0 := by
  rw [thickenedIndicator_apply, thickenedIndicatorAux_zero Œ¥_pos E x_out, toNNReal_zero]

theorem indicator_le_thickenedIndicator {Œ¥ : ‚Ñù} (Œ¥_pos : 0 < Œ¥) (E : Set Œ±) :
    (E.indicator fun _ => (1 : ‚Ñù‚â•0)) ‚â§ thickenedIndicator Œ¥_pos E := by
  intro a
  by_cases h : a ‚àà E
  ¬∑ simp only [h, indicator_of_mem, thickenedIndicator_one Œ¥_pos E h, le_refl]
  ¬∑ simp only [h, indicator_of_notMem, not_false_iff, zero_le]

theorem thickenedIndicator_mono {Œ¥‚ÇÅ Œ¥‚ÇÇ : ‚Ñù} (Œ¥‚ÇÅ_pos : 0 < Œ¥‚ÇÅ) (Œ¥‚ÇÇ_pos : 0 < Œ¥‚ÇÇ) (hle : Œ¥‚ÇÅ ‚â§ Œ¥‚ÇÇ)
    (E : Set Œ±) : ‚áë(thickenedIndicator Œ¥‚ÇÅ_pos E) ‚â§ thickenedIndicator Œ¥‚ÇÇ_pos E := by
  intro x
  apply (toNNReal_le_toNNReal thickenedIndicatorAux_lt_top.ne thickenedIndicatorAux_lt_top.ne).mpr
  apply thickenedIndicatorAux_mono hle

theorem thickenedIndicator_subset {Œ¥ : ‚Ñù} (Œ¥_pos : 0 < Œ¥) {E‚ÇÅ E‚ÇÇ : Set Œ±} (subset : E‚ÇÅ ‚äÜ E‚ÇÇ) :
    ‚áë(thickenedIndicator Œ¥_pos E‚ÇÅ) ‚â§ thickenedIndicator Œ¥_pos E‚ÇÇ := fun x =>
  (toNNReal_le_toNNReal thickenedIndicatorAux_lt_top.ne thickenedIndicatorAux_lt_top.ne).mpr
    (thickenedIndicatorAux_subset Œ¥ subset x)

/-- As the thickening radius Œ¥ tends to 0, the Œ¥-thickened indicator of a set E (in Œ±) tends
pointwise to the indicator function of the closure of E.

Note: This version is for the bundled bounded continuous functions, but the topology is not
the topology on `Œ± ‚Üí·µá ‚Ñù‚â•0`. Coercions to functions `Œ± ‚Üí ‚Ñù‚â•0` are done first, so the topology
instance is the product topology (the topology of pointwise convergence). -/
theorem thickenedIndicator_tendsto_indicator_closure {Œ¥seq : ‚Ñï ‚Üí ‚Ñù} (Œ¥seq_pos : ‚àÄ n, 0 < Œ¥seq n)
    (Œ¥seq_lim : Tendsto Œ¥seq atTop (ùìù 0)) (E : Set Œ±) :
    Tendsto (fun n : ‚Ñï => ((‚Üë) : (Œ± ‚Üí·µá ‚Ñù‚â•0) ‚Üí Œ± ‚Üí ‚Ñù‚â•0) (thickenedIndicator (Œ¥seq_pos n) E)) atTop
      (ùìù (indicator (closure E) fun _ => (1 : ‚Ñù‚â•0))) := by
  have key := thickenedIndicatorAux_tendsto_indicator_closure Œ¥seq_lim E
  rw [tendsto_pi_nhds] at *
  intro x
  rw [show indicator (closure E) (fun _ => (1 : ‚Ñù‚â•0)) x =
        (indicator (closure E) (fun _ => (1 : ‚Ñù‚â•0‚àû)) x).toNNReal
      by refine (congr_fun (comp_indicator_const 1 ENNReal.toNNReal toNNReal_zero) x).symm]
  refine Tendsto.comp (tendsto_toNNReal ?_) (key x)
  by_cases x_mem : x ‚àà closure E <;> simp [x_mem]

end thickenedIndicator

section indicator

variable {Œ± : Type*} [PseudoEMetricSpace Œ±] {Œ≤ : Type*} [One Œ≤]

/-- Pointwise, the multiplicative indicators of Œ¥-thickenings of a set eventually coincide
with the multiplicative indicator of the set as Œ¥>0 tends to zero. -/
@[to_additive "Pointwise, the indicators of Œ¥-thickenings of a set eventually coincide
with the indicator of the set as Œ¥>0 tends to zero."]
lemma mulIndicator_thickening_eventually_eq_mulIndicator_closure (f : Œ± ‚Üí Œ≤) (E : Set Œ±) (x : Œ±) :
    ‚àÄ·∂† Œ¥ in ùìù[>] (0 : ‚Ñù),
      (Metric.thickening Œ¥ E).mulIndicator f x = (closure E).mulIndicator f x := by
  by_cases x_mem_closure : x ‚àà closure E
  ¬∑ filter_upwards [self_mem_nhdsWithin] with Œ¥ Œ¥_pos
    simp only [closure_subset_thickening Œ¥_pos E x_mem_closure, mulIndicator_of_mem, x_mem_closure]
  ¬∑ have obs := eventually_notMem_thickening_of_infEdist_pos x_mem_closure
    filter_upwards [mem_nhdsWithin_of_mem_nhds obs, self_mem_nhdsWithin]
      with Œ¥ x_notin_thE _
    simp only [x_notin_thE, not_false_eq_true, mulIndicator_of_notMem, x_mem_closure]

/-- Pointwise, the multiplicative indicators of closed Œ¥-thickenings of a set eventually coincide
with the multiplicative indicator of the set as Œ¥ tends to zero. -/
@[to_additive "Pointwise, the indicators of closed Œ¥-thickenings of a set eventually coincide
with the indicator of the set as Œ¥ tends to zero."]
lemma mulIndicator_cthickening_eventually_eq_mulIndicator_closure (f : Œ± ‚Üí Œ≤) (E : Set Œ±) (x : Œ±) :
    ‚àÄ·∂† Œ¥ in ùìù (0 : ‚Ñù),
      (Metric.cthickening Œ¥ E).mulIndicator f x = (closure E).mulIndicator f x := by
  by_cases x_mem_closure : x ‚àà closure E
  ¬∑ filter_upwards [univ_mem] with Œ¥ _
    have obs : x ‚àà cthickening Œ¥ E := closure_subset_cthickening Œ¥ E x_mem_closure
    rw [mulIndicator_of_mem obs f, mulIndicator_of_mem x_mem_closure f]
  ¬∑ filter_upwards [eventually_notMem_cthickening_of_infEdist_pos x_mem_closure] with Œ¥ hŒ¥
    simp only [hŒ¥, not_false_eq_true, mulIndicator_of_notMem, x_mem_closure]

variable [TopologicalSpace Œ≤]

/-- The multiplicative indicators of Œ¥-thickenings of a set tend pointwise to the multiplicative
indicator of the set, as Œ¥>0 tends to zero. -/
@[to_additive "The indicators of Œ¥-thickenings of a set tend pointwise to the indicator of the
set, as Œ¥>0 tends to zero."]
lemma tendsto_mulIndicator_thickening_mulIndicator_closure (f : Œ± ‚Üí Œ≤) (E : Set Œ±) :
    Tendsto (fun Œ¥ ‚Ü¶ (Metric.thickening Œ¥ E).mulIndicator f) (ùìù[>] 0)
      (ùìù ((closure E).mulIndicator f)) := by
  rw [tendsto_pi_nhds]
  intro x
  rw [tendsto_congr' (mulIndicator_thickening_eventually_eq_mulIndicator_closure f E x)]
  apply tendsto_const_nhds

/-- The multiplicative indicators of closed Œ¥-thickenings of a set tend pointwise to the
multiplicative indicator of the set, as Œ¥ tends to zero. -/
@[to_additive "The indicators of closed Œ¥-thickenings of a set tend pointwise to the indicator
of the set, as Œ¥ tends to zero."]
lemma tendsto_mulIndicator_cthickening_mulIndicator_closure (f : Œ± ‚Üí Œ≤) (E : Set Œ±) :
    Tendsto (fun Œ¥ ‚Ü¶ (Metric.cthickening Œ¥ E).mulIndicator f) (ùìù 0)
      (ùìù ((closure E).mulIndicator f)) := by
  rw [tendsto_pi_nhds]
  intro x
  rw [tendsto_congr' (mulIndicator_cthickening_eventually_eq_mulIndicator_closure f E x)]
  apply tendsto_const_nhds

end indicator
