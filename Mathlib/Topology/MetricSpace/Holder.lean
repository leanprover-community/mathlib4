/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
module

public import Mathlib.Topology.MetricSpace.Lipschitz
public import Mathlib.Analysis.SpecialFunctions.Pow.Continuity
public import Mathlib.Analysis.Convex.NNReal

/-!
# H√∂lder continuous functions

In this file we define H√∂lder continuity on a set and on the whole space. We also prove some basic
properties of H√∂lder continuous functions.

## Main definitions

* `HolderOnWith`: `f : X ‚Üí Y` is said to be *H√∂lder continuous* with constant `C : ‚Ñù‚â•0` and
  exponent `r : ‚Ñù‚â•0` on a set `s`, if `edist (f x) (f y) ‚â§ C * edist x y ^ r` for all `x y ‚àà s`;
* `HolderWith`: `f : X ‚Üí Y` is said to be *H√∂lder continuous* with constant `C : ‚Ñù‚â•0` and exponent
  `r : ‚Ñù‚â•0`, if `edist (f x) (f y) ‚â§ C * edist x y ^ r` for all `x y : X`.

## Implementation notes

We use the type `‚Ñù‚â•0` (a.k.a. `NNReal`) for `C` because this type has coercion both to `‚Ñù` and
`‚Ñù‚â•0‚àû`, so it can be easily used both in inequalities about `dist` and `edist`. We also use `‚Ñù‚â•0`
for `r` to ensure that `d ^ r` is monotone in `d`. It might be a good idea to use
`‚Ñù>0` for `r` but we don't have this type in `mathlib` (yet).

## Tags

H√∂lder continuity, Lipschitz continuity

-/

@[expose] public section


variable {X Y Z : Type*}

open Filter Set Metric
open scoped NNReal ENNReal Topology

section EMetric

variable [PseudoEMetricSpace X] [PseudoEMetricSpace Y] [PseudoEMetricSpace Z]

/-- A function `f : X ‚Üí Y` between two `PseudoEMetricSpace`s is H√∂lder continuous with constant
`C : ‚Ñù‚â•0` and exponent `r : ‚Ñù‚â•0`, if `edist (f x) (f y) ‚â§ C * edist x y ^ r` for all `x y : X`. -/
def HolderWith (C r : ‚Ñù‚â•0) (f : X ‚Üí Y) : Prop :=
  ‚àÄ x y, edist (f x) (f y) ‚â§ (C : ‚Ñù‚â•0‚àû) * edist x y ^ (r : ‚Ñù)

/-- A function `f : X ‚Üí Y` between two `PseudoEMetricSpace`s is H√∂lder continuous with constant
`C : ‚Ñù‚â•0` and exponent `r : ‚Ñù‚â•0` on a set `s : Set X`, if `edist (f x) (f y) ‚â§ C * edist x y ^ r`
for all `x y ‚àà s`. -/
def HolderOnWith (C r : ‚Ñù‚â•0) (f : X ‚Üí Y) (s : Set X) : Prop :=
  ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, edist (f x) (f y) ‚â§ (C : ‚Ñù‚â•0‚àû) * edist x y ^ (r : ‚Ñù)

@[simp]
theorem holderOnWith_empty (C r : ‚Ñù‚â•0) (f : X ‚Üí Y) : HolderOnWith C r f ‚àÖ := fun _ hx => hx.elim

@[simp]
theorem holderOnWith_singleton (C r : ‚Ñù‚â•0) (f : X ‚Üí Y) (x : X) : HolderOnWith C r f {x} := by
  rintro a (rfl : a = x) b (rfl : b = a)
  rw [edist_self]
  exact zero_le _

theorem Set.Subsingleton.holderOnWith {s : Set X} (hs : s.Subsingleton) (C r : ‚Ñù‚â•0) (f : X ‚Üí Y) :
    HolderOnWith C r f s :=
  hs.induction_on (holderOnWith_empty C r f) (holderOnWith_singleton C r f)

theorem holderOnWith_univ {C r : ‚Ñù‚â•0} {f : X ‚Üí Y} : HolderOnWith C r f univ ‚Üî HolderWith C r f := by
  simp only [HolderOnWith, HolderWith, mem_univ, true_imp_iff]

@[simp]
theorem holderOnWith_one {C : ‚Ñù‚â•0} {f : X ‚Üí Y} {s : Set X} :
    HolderOnWith C 1 f s ‚Üî LipschitzOnWith C f s := by
  simp only [HolderOnWith, LipschitzOnWith, NNReal.coe_one, ENNReal.rpow_one]

alias ‚ü®_, LipschitzOnWith.holderOnWith‚ü© := holderOnWith_one

@[simp]
theorem holderWith_one {C : ‚Ñù‚â•0} {f : X ‚Üí Y} : HolderWith C 1 f ‚Üî LipschitzWith C f :=
  holderOnWith_univ.symm.trans <| holderOnWith_one.trans lipschitzOnWith_univ

alias ‚ü®_, LipschitzWith.holderWith‚ü© := holderWith_one

theorem holderWith_id : HolderWith 1 1 (id : X ‚Üí X) :=
  LipschitzWith.id.holderWith

protected theorem HolderWith.holderOnWith {C r : ‚Ñù‚â•0} {f : X ‚Üí Y} (h : HolderWith C r f)
    (s : Set X) : HolderOnWith C r f s := fun x _ y _ => h x y

namespace HolderOnWith

variable {C r : ‚Ñù‚â•0} {f : X ‚Üí Y} {s t : Set X}

theorem edist_le (h : HolderOnWith C r f s) {x y : X} (hx : x ‚àà s) (hy : y ‚àà s) :
    edist (f x) (f y) ‚â§ (C : ‚Ñù‚â•0‚àû) * edist x y ^ (r : ‚Ñù) :=
  h x hx y hy

theorem edist_le_of_le (h : HolderOnWith C r f s) {x y : X} (hx : x ‚àà s) (hy : y ‚àà s) {d : ‚Ñù‚â•0‚àû}
    (hd : edist x y ‚â§ d) : edist (f x) (f y) ‚â§ (C : ‚Ñù‚â•0‚àû) * d ^ (r : ‚Ñù) :=
  (h.edist_le hx hy).trans <| by gcongr

theorem comp {Cg rg : ‚Ñù‚â•0} {g : Y ‚Üí Z} {t : Set Y} (hg : HolderOnWith Cg rg g t) {Cf rf : ‚Ñù‚â•0}
    {f : X ‚Üí Y} (hf : HolderOnWith Cf rf f s) (hst : MapsTo f s t) :
    HolderOnWith (Cg * Cf ^ (rg : ‚Ñù)) (rg * rf) (g ‚àò f) s := by
  intro x hx y hy
  rw [ENNReal.coe_mul, mul_comm rg, NNReal.coe_mul, ENNReal.rpow_mul, mul_assoc,
    ENNReal.coe_rpow_of_nonneg _ rg.coe_nonneg, ‚Üê ENNReal.mul_rpow_of_nonneg _ _ rg.coe_nonneg]
  exact hg.edist_le_of_le (hst hx) (hst hy) (hf.edist_le hx hy)

theorem comp_holderWith {Cg rg : ‚Ñù‚â•0} {g : Y ‚Üí Z} {t : Set Y} (hg : HolderOnWith Cg rg g t)
    {Cf rf : ‚Ñù‚â•0} {f : X ‚Üí Y} (hf : HolderWith Cf rf f) (ht : ‚àÄ x, f x ‚àà t) :
    HolderWith (Cg * Cf ^ (rg : ‚Ñù)) (rg * rf) (g ‚àò f) :=
  holderOnWith_univ.mp <| hg.comp (hf.holderOnWith univ) fun x _ => ht x

/-- A H√∂lder continuous function is uniformly continuous -/
protected theorem uniformContinuousOn (hf : HolderOnWith C r f s) (h0 : 0 < r) :
    UniformContinuousOn f s := by
  refine EMetric.uniformContinuousOn_iff.2 fun Œµ Œµpos => ?_
  have : Tendsto (fun d : ‚Ñù‚â•0‚àû => (C : ‚Ñù‚â•0‚àû) * d ^ (r : ‚Ñù)) (ùìù 0) (ùìù 0) :=
    ENNReal.tendsto_const_mul_rpow_nhds_zero_of_pos ENNReal.coe_ne_top h0
  rcases ENNReal.nhds_zero_basis.mem_iff.1 (this (gt_mem_nhds Œµpos)) with ‚ü®Œ¥, Œ¥0, H‚ü©
  exact ‚ü®Œ¥, Œ¥0, fun hx y hy h => (hf.edist_le hx hy).trans_lt (H h)‚ü©

protected theorem continuousOn (hf : HolderOnWith C r f s) (h0 : 0 < r) : ContinuousOn f s :=
  (hf.uniformContinuousOn h0).continuousOn

protected theorem mono (hf : HolderOnWith C r f s) (ht : t ‚äÜ s) : HolderOnWith C r f t :=
  fun _ hx _ hy => hf.edist_le (ht hx) (ht hy)

theorem ediam_image_le_of_le (hf : HolderOnWith C r f s) {d : ‚Ñù‚â•0‚àû} (hd : ediam s ‚â§ d) :
    ediam (f '' s) ‚â§ (C : ‚Ñù‚â•0‚àû) * d ^ (r : ‚Ñù) :=
  ediam_image_le_iff.2 fun _ hx _ hy =>
    hf.edist_le_of_le hx hy <| (edist_le_ediam_of_mem hx hy).trans hd

theorem ediam_image_le (hf : HolderOnWith C r f s) :
    ediam (f '' s) ‚â§ (C : ‚Ñù‚â•0‚àû) * ediam s ^ (r : ‚Ñù) :=
  hf.ediam_image_le_of_le le_rfl

theorem ediam_image_le_of_subset (hf : HolderOnWith C r f s) (ht : t ‚äÜ s) :
    ediam (f '' t) ‚â§ (C : ‚Ñù‚â•0‚àû) * ediam t ^ (r : ‚Ñù) :=
  (hf.mono ht).ediam_image_le

theorem ediam_image_le_of_subset_of_le (hf : HolderOnWith C r f s) (ht : t ‚äÜ s) {d : ‚Ñù‚â•0‚àû}
    (hd : ediam t ‚â§ d) : ediam (f '' t) ‚â§ (C : ‚Ñù‚â•0‚àû) * d ^ (r : ‚Ñù) :=
  (hf.mono ht).ediam_image_le_of_le hd

theorem ediam_image_inter_le_of_le (hf : HolderOnWith C r f s) {d : ‚Ñù‚â•0‚àû}
    (hd : ediam t ‚â§ d) : ediam (f '' (t ‚à© s)) ‚â§ (C : ‚Ñù‚â•0‚àû) * d ^ (r : ‚Ñù) :=
  hf.ediam_image_le_of_subset_of_le inter_subset_right <|
    (ediam_mono inter_subset_left).trans hd

theorem ediam_image_inter_le (hf : HolderOnWith C r f s) (t : Set X) :
    ediam (f '' (t ‚à© s)) ‚â§ (C : ‚Ñù‚â•0‚àû) * ediam t ^ (r : ‚Ñù) :=
  hf.ediam_image_inter_le_of_le le_rfl

/-- If a function is `(C‚ÇÅ, r)`-H√∂lder and `(C‚ÇÇ, s)`-H√∂lder, then it is
`(C‚ÇÅ ^ t‚ÇÅ * C‚ÇÇ ^ t‚ÇÇ, r * t‚ÇÅ + s * t‚ÇÇ)`-H√∂lder for all `t‚ÇÅ t‚ÇÇ : ‚Ñù‚â•0` such that
`t‚ÇÅ + t‚ÇÇ = 1`. -/
lemma interpolate {C‚ÇÅ C‚ÇÇ s t‚ÇÅ t‚ÇÇ : ‚Ñù‚â•0} {A : Set X}
    (hf‚ÇÅ : HolderOnWith C‚ÇÅ r f A) (hf‚ÇÇ : HolderOnWith C‚ÇÇ s f A) (ht : t‚ÇÅ + t‚ÇÇ = 1) :
    HolderOnWith (C‚ÇÅ ^ (t‚ÇÅ : ‚Ñù) * C‚ÇÇ ^ (t‚ÇÇ : ‚Ñù)) (r * t‚ÇÅ + s * t‚ÇÇ) f A := by
  intro x hx y hy
  calc edist (f x) (f y)
      = (edist (f x) (f y)) ^ (t‚ÇÅ : ‚Ñù) * (edist (f x) (f y)) ^ (t‚ÇÇ : ‚Ñù) := by
        simp [‚Üê ENNReal.rpow_add_of_nonneg, ‚Üê NNReal.coe_add, ht]
    _ ‚â§ (C‚ÇÅ * (edist x y) ^ (r : ‚Ñù)) ^ (t‚ÇÅ : ‚Ñù) * (C‚ÇÇ * (edist x y) ^ (s : ‚Ñù)) ^ (t‚ÇÇ : ‚Ñù) := by
        nth_grw 1 [hf‚ÇÅ x hx y hy, hf‚ÇÇ x hx y hy]
    _ = ‚Üë(C‚ÇÅ ^ (t‚ÇÅ : ‚Ñù) * C‚ÇÇ ^ (t‚ÇÇ : ‚Ñù)) * (edist x y) ^ (‚Üë(r * t‚ÇÅ + s * t‚ÇÇ) : ‚Ñù) := by
        push_cast
        simp (discharger := positivity) only [ENNReal.mul_rpow_of_nonneg,
          ENNReal.rpow_add_of_nonneg, ENNReal.rpow_mul, ENNReal.coe_rpow_of_nonneg]
        ring

/-- If a function is H√∂lder over a bounded set, then it is bounded. -/
lemma holderOnWith_zero_of_bounded {C D : ‚Ñù‚â•0} {A : Set X}
    (hA : ‚àÄ x ‚àà A, ‚àÄ y ‚àà A, edist x y ‚â§ D) (hf : HolderOnWith C r f A) :
    HolderOnWith (C * D ^ (r : ‚Ñù)) 0 f A := by
  intro x hx y hy
  simp only [NNReal.coe_zero, ENNReal.rpow_zero, mul_one]
  grw [hf x hx y hy, hA x hx y hy, ENNReal.coe_mul, ENNReal.coe_rpow_of_nonneg _ (by simp)]

/-- If a function is `r`-H√∂lder over a bounded set, then it is also `s`-H√∂lder when `s ‚â§ r`. -/
lemma of_le {C D s : ‚Ñù‚â•0} {A : Set X}
    (hA : ‚àÄ x ‚àà A, ‚àÄ y ‚àà A, edist x y ‚â§ D) (hf : HolderOnWith C r f A) (hsr : s ‚â§ r) :
    HolderOnWith (C * D ^ (r - s : ‚Ñù)) s f A := by
  obtain rfl | ht := eq_zero_or_pos s
  ¬∑ simpa using hf.holderOnWith_zero_of_bounded hA
  have hr : 0 < r := ht.trans_le hsr
  rw [‚Üê NNReal.coe_le_coe] at hsr
  rw [‚Üê NNReal.coe_pos] at hr
  set Œ∏‚ÇÅ : ‚Ñù‚â•0 := ‚ü®s/r, by positivity‚ü©
  set Œ∏‚ÇÇ : ‚Ñù‚â•0 := ‚ü®1 - s/r, by simpa using div_le_one_of_le‚ÇÄ hsr (by positivity)‚ü©
  have hŒ∏ : Œ∏‚ÇÅ + Œ∏‚ÇÇ = 1 := by ext; simp [Œ∏‚ÇÅ, Œ∏‚ÇÇ]
  have hŒ∏t : r * Œ∏‚ÇÅ + 0 * Œ∏‚ÇÇ = s := by ext; simp [Œ∏‚ÇÅ, mul_div_cancel‚ÇÄ _ hr.ne']
  have hŒ∏C : C * D ^ (r - s : ‚Ñù) = C ^ (Œ∏‚ÇÅ : ‚Ñù) * (C * D ^ (r : ‚Ñù)) ^ (Œ∏‚ÇÇ : ‚Ñù) := by
    simp (discharger := positivity) only [NNReal.mul_rpow, ‚Üê mul_assoc,
      ‚Üê NNReal.rpow_add_of_nonneg, ‚Üê NNReal.rpow_mul, ‚Üê NNReal.coe_add, hŒ∏, NNReal.coe_one,
      NNReal.rpow_one]
    congr
    simp [mul_sub, Œ∏‚ÇÇ, mul_div_cancel‚ÇÄ _ hr.ne']
  rw [hŒ∏C, ‚Üê hŒ∏t]
  exact hf.interpolate (hf.holderOnWith_zero_of_bounded hA) hŒ∏

lemma mono_const {C‚ÇÅ C‚ÇÇ : ‚Ñù‚â•0} {A : Set X} (hf : HolderOnWith C‚ÇÅ r f A)
    (hC : C‚ÇÅ ‚â§ C‚ÇÇ) : HolderOnWith C‚ÇÇ r f A := by
  intro x hx y hy
  grw [‚Üê hC]
  exact hf x hx y hy

/-- If a function is `(C, r)`-H√∂lder and `(C, s)`-H√∂lder,
then it is `(C, r * t‚ÇÅ + s * t‚ÇÇ)`-H√∂lder for all `t‚ÇÅ t‚ÇÇ : ‚Ñù‚â•0` such that
`t‚ÇÅ + t‚ÇÇ = 1`. -/
lemma interpolate_const {C s t‚ÇÅ t‚ÇÇ : ‚Ñù‚â•0} {A : Set X}
    (hf‚ÇÅ : HolderOnWith C r f A) (hf‚ÇÇ : HolderOnWith C s f A) (ht : t‚ÇÅ + t‚ÇÇ = 1) :
    HolderOnWith C (r * t‚ÇÅ + s * t‚ÇÇ) f A := by
  convert hf‚ÇÅ.interpolate hf‚ÇÇ ht
  simp [‚Üê NNReal.rpow_add_of_nonneg, ‚Üê NNReal.coe_add, ht]

variable (f) in
/-- For fixed `f : X ‚Üí Y`, `A : Set X` and `C : ‚Ñù‚â•0`, the set of all parameters `r : ‚Ñù‚â•0` such that
`f` is `(C, r)`-H√∂lder on `A` is convex. -/
lemma _root_.convex_setOf_holderOnWith (C : ‚Ñù‚â•0) (A : Set X) :
    Convex ‚Ñù‚â•0 {r | HolderOnWith C r f A} := by
  intro r hr s hs _ _ _ _ ht
  rw [smul_eq_mul, smul_eq_mul, ‚Üê mul_comm r, ‚Üê mul_comm s]
  exact hr.interpolate_const hs ht

lemma of_le_of_le {C‚ÇÅ C‚ÇÇ s t : ‚Ñù‚â•0} {A : Set X}
    (hf‚ÇÅ : HolderOnWith C‚ÇÅ r f A) (hf‚ÇÇ : HolderOnWith C‚ÇÇ s f A) (hrt : r ‚â§ t)
    (hts : t ‚â§ s) : HolderOnWith (max C‚ÇÅ C‚ÇÇ) t f A := by
  replace hf‚ÇÅ := hf‚ÇÅ.mono_const (le_max_left C‚ÇÅ C‚ÇÇ)
  replace hf‚ÇÇ := hf‚ÇÇ.mono_const (le_max_right C‚ÇÅ C‚ÇÇ)
  exact convex_setOf_holderOnWith f (max C‚ÇÅ C‚ÇÇ) A |>.segment_subset hf‚ÇÅ hf‚ÇÇ
    (NNReal.Icc_subset_segment ‚ü®hrt, hts‚ü©)

end HolderOnWith

namespace HolderWith

variable {C r : ‚Ñù‚â•0} {f : X ‚Üí Y}

theorem restrict_iff {s : Set X} : HolderWith C r (s.restrict f) ‚Üî HolderOnWith C r f s := by
  simp [HolderWith, HolderOnWith]

protected alias ‚ü®_, _root_.HolderOnWith.holderWith‚ü© := restrict_iff

theorem edist_le (h : HolderWith C r f) (x y : X) :
    edist (f x) (f y) ‚â§ (C : ‚Ñù‚â•0‚àû) * edist x y ^ (r : ‚Ñù) :=
  h x y

theorem edist_le_of_le (h : HolderWith C r f) {x y : X} {d : ‚Ñù‚â•0‚àû} (hd : edist x y ‚â§ d) :
    edist (f x) (f y) ‚â§ (C : ‚Ñù‚â•0‚àû) * d ^ (r : ‚Ñù) :=
  (h.holderOnWith univ).edist_le_of_le trivial trivial hd

theorem comp {Cg rg : ‚Ñù‚â•0} {g : Y ‚Üí Z} (hg : HolderWith Cg rg g) {Cf rf : ‚Ñù‚â•0} {f : X ‚Üí Y}
    (hf : HolderWith Cf rf f) : HolderWith (Cg * Cf ^ (rg : ‚Ñù)) (rg * rf) (g ‚àò f) :=
  (hg.holderOnWith univ).comp_holderWith hf fun _ => trivial

theorem comp_holderOnWith {Cg rg : ‚Ñù‚â•0} {g : Y ‚Üí Z} (hg : HolderWith Cg rg g) {Cf rf : ‚Ñù‚â•0}
    {f : X ‚Üí Y} {s : Set X} (hf : HolderOnWith Cf rf f s) :
    HolderOnWith (Cg * Cf ^ (rg : ‚Ñù)) (rg * rf) (g ‚àò f) s :=
  (hg.holderOnWith univ).comp hf fun _ _ => trivial

/-- A H√∂lder continuous function is uniformly continuous -/
protected theorem uniformContinuous (hf : HolderWith C r f) (h0 : 0 < r) : UniformContinuous f :=
  uniformContinuousOn_univ.mp <| (hf.holderOnWith univ).uniformContinuousOn h0

protected theorem continuous (hf : HolderWith C r f) (h0 : 0 < r) : Continuous f :=
  (hf.uniformContinuous h0).continuous

theorem ediam_image_le (hf : HolderWith C r f) (s : Set X) :
    ediam (f '' s) ‚â§ (C : ‚Ñù‚â•0‚àû) * ediam s ^ (r : ‚Ñù) :=
  ediam_image_le_iff.2 fun _ hx _ hy => hf.edist_le_of_le <| edist_le_ediam_of_mem hx hy

lemma const {y : Y} :
    HolderWith C r (Function.const X y) := fun x‚ÇÅ x‚ÇÇ => by
  simp only [Function.const_apply, edist_self, zero_le]

lemma zero [Zero Y] : HolderWith C r (0 : X ‚Üí Y) := .const

lemma of_isEmpty [IsEmpty X] : HolderWith C r f := isEmptyElim

lemma mono {C' : ‚Ñù‚â•0} (hf : HolderWith C r f) (h : C ‚â§ C') :
    HolderWith C' r f :=
  fun x‚ÇÅ x‚ÇÇ ‚Ü¶ (hf x‚ÇÅ x‚ÇÇ).trans (by gcongr)

/-- If a function is `(C‚ÇÅ, r)`-H√∂lder and `(C‚ÇÇ, s)`-H√∂lder, then it is
`(C‚ÇÅ ^ t‚ÇÅ * C‚ÇÇ ^ t‚ÇÇ, r * t‚ÇÅ + s * t‚ÇÇ)`-H√∂lder for all `t‚ÇÅ t‚ÇÇ : ‚Ñù‚â•0` such that
`t‚ÇÅ + t‚ÇÇ = 1`. -/
lemma interpolate {C‚ÇÅ C‚ÇÇ s t‚ÇÅ t‚ÇÇ : ‚Ñù‚â•0}
    (hf‚ÇÅ : HolderWith C‚ÇÅ r f) (hf‚ÇÇ : HolderWith C‚ÇÇ s f) (ht : t‚ÇÅ + t‚ÇÇ = 1) :
    HolderWith (C‚ÇÅ ^ (t‚ÇÅ : ‚Ñù) * C‚ÇÇ ^ (t‚ÇÇ : ‚Ñù)) (r * t‚ÇÅ + s * t‚ÇÇ) f :=
  holderOnWith_univ.1 ((holderOnWith_univ.2 hf‚ÇÅ).interpolate (holderOnWith_univ.2 hf‚ÇÇ) ht)

/-- If a function is H√∂lder over a bounded space, then it is bounded. -/
lemma holderWith_zero_of_bounded {C D : ‚Ñù‚â•0}
    (h : ‚àÄ x y : X, edist x y ‚â§ D) (hf : HolderWith C r f) :
    HolderWith (C * D ^ (r : ‚Ñù)) 0 f :=
  holderOnWith_univ.1 ((holderOnWith_univ.2 hf).holderOnWith_zero_of_bounded (fun x _ y _ ‚Ü¶ h x y))

/-- If a function is `r`-H√∂lder over a bounded space, then it is also `s`-H√∂lder when `s ‚â§ r`. -/
lemma of_le {C D s : ‚Ñù‚â•0} (h : ‚àÄ x y : X, edist x y ‚â§ D) (hf : HolderWith C r f) (hsr : s ‚â§ r) :
    HolderWith (C * D ^ (r - s : ‚Ñù)) s f :=
  holderOnWith_univ.1 ((holderOnWith_univ.2 hf).of_le (fun x _ y _ ‚Ü¶ h x y) hsr)

/-- If a function is `(C, r)`-H√∂lder and `(C, s)`-H√∂lder,
then it is `(C, r * t‚ÇÅ + s * t‚ÇÇ)`-H√∂lder for all `t‚ÇÅ t‚ÇÇ : ‚Ñù‚â•0` such that
`t‚ÇÅ + t‚ÇÇ = 1`. -/
lemma interpolate_const {C s t‚ÇÅ t‚ÇÇ : ‚Ñù‚â•0}
    (hf‚ÇÅ : HolderWith C r f) (hf‚ÇÇ : HolderWith C s f) (ht : t‚ÇÅ + t‚ÇÇ = 1) :
    HolderWith C (r * t‚ÇÅ + s * t‚ÇÇ) f :=
  holderOnWith_univ.1 ((holderOnWith_univ.2 hf‚ÇÅ).interpolate_const (holderOnWith_univ.2 hf‚ÇÇ) ht)

variable (f) in
/-- For fixed `f : X ‚Üí Y` and `C : ‚Ñù‚â•0`, the set of all parameters `r : ‚Ñù‚â•0` such that
`f` is `(C, r)`-H√∂lder is convex. -/
lemma _root_.convex_setOf_holderWith (C : ‚Ñù‚â•0) :
    Convex ‚Ñù‚â•0 {r | HolderWith C r f} := by
  simp_rw [‚Üê holderOnWith_univ]
  exact convex_setOf_holderOnWith f C _

lemma of_le_of_le {C‚ÇÅ C‚ÇÇ s t : ‚Ñù‚â•0}
    (hf‚ÇÅ : HolderWith C‚ÇÅ r f) (hf‚ÇÇ : HolderWith C‚ÇÇ s f) (hrt : r ‚â§ t)
    (hts : t ‚â§ s) : HolderWith (max C‚ÇÅ C‚ÇÇ) t f :=
  holderOnWith_univ.1 ((holderOnWith_univ.2 hf‚ÇÅ).of_le_of_le (holderOnWith_univ.2 hf‚ÇÇ) hrt hts)

end HolderWith

end EMetric

section PseudoMetric

variable [PseudoMetricSpace X] [PseudoMetricSpace Y] {C r : ‚Ñù‚â•0} {f : X ‚Üí Y} {s : Set X} {x y : X}

namespace HolderOnWith

theorem nndist_le_of_le (hf : HolderOnWith C r f s) (hx : x ‚àà s) (hy : y ‚àà s)
    {d : ‚Ñù‚â•0} (hd : nndist x y ‚â§ d) : nndist (f x) (f y) ‚â§ C * d ^ (r : ‚Ñù) := by
  rw [‚Üê ENNReal.coe_le_coe, ‚Üê edist_nndist, ENNReal.coe_mul,
    ENNReal.coe_rpow_of_nonneg _ r.coe_nonneg]
  apply hf.edist_le_of_le hx hy
  rwa [edist_nndist, ENNReal.coe_le_coe]

theorem nndist_le (hf : HolderOnWith C r f s) (hx : x ‚àà s) (hy : y ‚àà s) :
    nndist (f x) (f y) ‚â§ C * nndist x y ^ (r : ‚Ñù) :=
  hf.nndist_le_of_le hx hy le_rfl

theorem dist_le_of_le (hf : HolderOnWith C r f s) (hx : x ‚àà s) (hy : y ‚àà s)
    {d : ‚Ñù} (hd : dist x y ‚â§ d) : dist (f x) (f y) ‚â§ C * d ^ (r : ‚Ñù) := by
  lift d to ‚Ñù‚â•0 using dist_nonneg.trans hd
  rw [dist_nndist] at hd ‚ä¢
  norm_cast at hd ‚ä¢
  exact hf.nndist_le_of_le hx hy hd

theorem dist_le (hf : HolderOnWith C r f s) (hx : x ‚àà s) (hy : y ‚àà s) :
    dist (f x) (f y) ‚â§ C * dist x y ^ (r : ‚Ñù) :=
  hf.dist_le_of_le hx hy le_rfl

end HolderOnWith

namespace HolderWith

theorem nndist_le_of_le (hf : HolderWith C r f) {x y : X} {d : ‚Ñù‚â•0} (hd : nndist x y ‚â§ d) :
    nndist (f x) (f y) ‚â§ C * d ^ (r : ‚Ñù) :=
  (hf.holderOnWith univ).nndist_le_of_le (mem_univ x) (mem_univ y) hd

theorem nndist_le (hf : HolderWith C r f) (x y : X) :
    nndist (f x) (f y) ‚â§ C * nndist x y ^ (r : ‚Ñù) :=
  hf.nndist_le_of_le le_rfl

theorem dist_le_of_le (hf : HolderWith C r f) {x y : X} {d : ‚Ñù} (hd : dist x y ‚â§ d) :
    dist (f x) (f y) ‚â§ C * d ^ (r : ‚Ñù) :=
  (hf.holderOnWith univ).dist_le_of_le (mem_univ x) (mem_univ y) hd

theorem dist_le (hf : HolderWith C r f) (x y : X) : dist (f x) (f y) ‚â§ C * dist x y ^ (r : ‚Ñù) :=
  hf.dist_le_of_le le_rfl

end HolderWith

end PseudoMetric

section Metric

variable [PseudoMetricSpace X] [MetricSpace Y] {r : ‚Ñù‚â•0} {f : X ‚Üí Y}

@[simp]
lemma holderWith_zero_iff : HolderWith 0 r f ‚Üî ‚àÄ x‚ÇÅ x‚ÇÇ, f x‚ÇÅ = f x‚ÇÇ := by
  refine ‚ü®fun h x‚ÇÅ x‚ÇÇ => ?_, fun h x‚ÇÅ x‚ÇÇ => h x‚ÇÅ x‚ÇÇ ‚ñ∏ ?_‚ü©
  ¬∑ specialize h x‚ÇÅ x‚ÇÇ
    simp [ENNReal.coe_zero, zero_mul, nonpos_iff_eq_zero, edist_eq_zero] at h
    assumption
  ¬∑ simp only [edist_self, ENNReal.coe_zero, zero_mul, le_refl]

end Metric

section SeminormedAddCommGroup

variable [PseudoMetricSpace X] [SeminormedAddCommGroup Y] {C C' r : ‚Ñù‚â•0} {f g : X ‚Üí Y}

namespace HolderWith

lemma add (hf : HolderWith C r f) (hg : HolderWith C' r g) :
    HolderWith (C + C') r (f + g) := by
  intro x‚ÇÅ x‚ÇÇ
  simp only [Pi.add_apply, ENNReal.coe_add]
  grw [edist_add_add_le, hf x‚ÇÅ x‚ÇÇ, hg x‚ÇÅ x‚ÇÇ]
  rw [add_mul]

lemma smul {Œ±} [SeminormedAddCommGroup Œ±] [SMulZeroClass Œ± Y] [IsBoundedSMul Œ± Y] (a : Œ±)
    (hf : HolderWith C r f) : HolderWith (C * ‚Äña‚Äñ‚Çä) r (a ‚Ä¢ f) := fun x‚ÇÅ x‚ÇÇ => by
  refine edist_smul_le _ _ _ |>.trans ?_
  rw [ENNReal.coe_mul, ENNReal.smul_def, smul_eq_mul, mul_comm (C : ‚Ñù‚â•0‚àû), mul_assoc]
  gcongr
  exact hf x‚ÇÅ x‚ÇÇ

lemma smul_iff {Œ±} [SeminormedRing Œ±] [Module Œ± Y] [NormSMulClass Œ± Y] (a : Œ±)
    (ha : ‚Äña‚Äñ‚Çä ‚â† 0) :
    HolderWith (C * ‚Äña‚Äñ‚Çä) r (a ‚Ä¢ f) ‚Üî HolderWith C r f := by
  simp_rw [HolderWith, ENNReal.coe_mul, Pi.smul_apply, edist_smul‚ÇÄ, ENNReal.smul_def, smul_eq_mul,
    mul_comm (C : ‚Ñù‚â•0‚àû), mul_assoc,
    ENNReal.mul_le_mul_iff_right (ENNReal.coe_ne_zero.mpr ha) ENNReal.coe_ne_top, mul_comm]

end HolderWith

end SeminormedAddCommGroup
