/-
Copyright (c) 2025 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
module

public import Mathlib.Analysis.SpecialFunctions.Pow.NNReal
public import Mathlib.Topology.EMetricSpace.Paracompact
public import Mathlib.Topology.Separation.CompletelyRegular
import Mathlib.Analysis.MeanInequalitiesPow

/-!
# A copy of a metric space with metric given by `d x y = dist x y ^ Œ±`

Given a (pseudo) (extended) metric space `X` and a number `0 < Œ± < 1`,
one can consider the metric given by `d x y = (dist x y) ^ Œ±`.
In this file we define `WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ` to be a one-field structure wrapper around `X`
with metric given by this formula.

One of the reasons to introduce this definition is the following.
In the proof of his version of the Morse-Sard theorem,
Moreira [Moreira2001] studies maps of two variables that are Lipschitz continuous in one variable,
but satisfy a stronger assumption `‚Äñf (a, y) - f (a, b)‚Äñ = O(‚Äñy - b‚Äñ ^ Œ±)`
along the second variable, as long as `(a, b)` is one of the "interesting" points.

If we want to apply Vitali family in this context, we need to cover the set by products
`closedBall a (R ^ Œ±) √óÀ¢ closedBall b R` so that both components make a similar contribution
to `‚Äñf (x, y) - f (a, b)‚Äñ`. These sets aren't balls in the original metric
(or even subsets of balls that occupy at least a fixed fraction of the volume,
as we require in our version of Vitali theorem).

However, if we change the metric on the first component to the one introduced in this file,
then these sets become balls, and we can apply Vitali theorem.

## References
* [Carlos Gustavo T. de A. Moreira, _Hausdorff measures and the Morse-Sard theorem_]
  [Moreira2001]
-/

@[expose] public section

open scoped ENNReal NNReal Filter Uniformity Topology
open Function

noncomputable section

/-- A copy of a type with metric given by `dist x y = (dist x.val y.val) ^ Œ±`.

This is defined as a one-field structure. -/
@[ext]
structure WithRPowDist (X : Type*) (Œ± : ‚Ñù) (hŒ±‚ÇÄ : 0 < Œ±) (hŒ±‚ÇÅ : Œ± ‚â§ 1) where
  /-- The value wrapped in `x : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ`. -/
  val : X

namespace WithRPowDist

variable {X : Type*} {Œ± : ‚Ñù} {hŒ±‚ÇÄ : 0 < Œ±} {hŒ±‚ÇÅ : Œ± ‚â§ 1}

/-- The natural equivalence between `WithRPowDist X Œ± hr‚ÇÄ hr‚ÇÅ` and `X`. -/
@[simps -fullyApplied apply symm_apply]
def equiv (X : Type*) (Œ± : ‚Ñù) (hr‚ÇÄ : 0 < Œ±) (hr‚ÇÅ : Œ± ‚â§ 1) : WithRPowDist X Œ± hr‚ÇÄ hr‚ÇÅ ‚âÉ X where
  toFun := val
  invFun := mk
  left_inv _ := rfl
  right_inv _ := rfl

@[simp]
theorem val_comp_mk : (val : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ ‚Üí X) ‚àò mk = id := rfl

@[simp]
theorem mk_comp_val : (mk : X ‚Üí WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) ‚àò val = id := rfl

theorem image_mk_eq_preimage (s : Set X) :
    (mk '' s : Set (WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ)) = val ‚Åª¬π' s :=
  (equiv X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ).symm.image_eq_preimage_symm _

theorem image_val_eq_preimage (s : Set (WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ)) :
    val '' s = mk ‚Åª¬π' s :=
  (equiv X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ).image_eq_preimage_symm _

@[simp]
theorem image_mk_image_val (s : Set (WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ)) :
    mk '' (val '' s) = s :=
  (equiv X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ).symm_image_image _

@[simp]
theorem image_val_image_mk (s : Set X) : val '' (mk '' s : Set (WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ)) = s :=
  (equiv X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ).image_symm_image _

theorem surjective_val : Surjective (val : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ ‚Üí X) :=
  equiv _ _ _ _ |>.surjective

theorem surjective_mk : Surjective (mk :  X ‚Üí WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) :=
  equiv _ _ _ _ |>.symm |>.surjective

theorem injective_mk : Injective (mk : X ‚Üí WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) := by
  simp [Injective]

/-!
### Topological space structure

The topology on `WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ` is induced from `X`.
-/

section TopologicalSpace

variable [TopologicalSpace X]

/-- The topological space structure on `WithRPowDist X Œ± _ _` is induced from the original space. -/
instance : TopologicalSpace (WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) := .induced WithRPowDist.val ‚Äπ_‚Ä∫

@[fun_prop]
theorem continuous_val : Continuous (val : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ ‚Üí X) :=
  continuous_induced_dom

@[fun_prop]
theorem continuous_mk : Continuous (mk : X ‚Üí WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) :=
  continuous_induced_rng.2 continuous_id

/-- The natural homeomorphism between `WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ` and `X`. -/
@[simps! -fullyApplied toEquiv apply symm_apply]
def homeomorph : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ ‚âÉ‚Çú X where
  toEquiv := WithRPowDist.equiv X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ

/-!
We copy some instances from the underlying space `X` to `WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ`.
In the future, we can add more of them, if needed,
or even copy all the topology-related classes, if we get a tactic to do it automatically.
-/

instance [T0Space X] : T0Space (WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) :=
  homeomorph.symm.t0Space

instance [T2Space X] : T2Space (WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) :=
  homeomorph.symm.t2Space

instance [SecondCountableTopology X] : SecondCountableTopology (WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) :=
  homeomorph.secondCountableTopology

end TopologicalSpace

/-!
### Bornology

The bornology on `WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ` is induced from `X`.
-/

section Bornology

variable [Bornology X]

instance : Bornology (WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) := .induced val

open Bornology

@[simp]
theorem isBounded_image_val_iff {s : Set (WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ)} :
    IsBounded (val '' s) ‚Üî IsBounded s :=
  isBounded_induced.symm

@[simp]
theorem isBounded_preimage_mk_iff {s : Set (WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ)} :
    IsBounded (mk ‚Åª¬π' s) ‚Üî IsBounded s := by
  rw [‚Üê image_val_eq_preimage, isBounded_image_val_iff]

@[simp]
theorem isBounded_image_mk_iff {s : Set X} :
    IsBounded (mk '' s : Set (WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ)) ‚Üî IsBounded s := by
  rw [‚Üê isBounded_image_val_iff, image_val_image_mk]

@[simp]
theorem isBounded_preimage_val_iff {s : Set X} :
    IsBounded (val ‚Åª¬π' s : Set (WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ)) ‚Üî IsBounded s := by
  rw [‚Üê image_mk_eq_preimage, isBounded_image_mk_iff]

end Bornology

/-!
### Uniform space structure

The uniform space structure on `WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ` is induced from `X`.
-/

section UniformSpace

variable [UniformSpace X]

instance : UniformSpace (WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) :=
  UniformSpace.comap WithRPowDist.val ‚Äπ_‚Ä∫

theorem uniformContinuous_val : UniformContinuous (val : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ ‚Üí X) :=
  uniformContinuous_comap

theorem uniformContinuous_mk : UniformContinuous (mk : X ‚Üí WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) :=
  uniformContinuous_comap' uniformContinuous_id

/-
Define a UniformEquiv between this space and X.
-/
@[simps! toEquiv apply symm_apply]
def uniformEquiv : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ ‚âÉ·µ§ X where
  toEquiv := WithRPowDist.equiv X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ
  uniformContinuous_toFun := uniformContinuous_val
  uniformContinuous_invFun := uniformContinuous_mk

end UniformSpace

/-!
### Extended distance and a (pseudo) extended metric space structure

Th extended distance on `WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ`
is given by `edist x y = (edist x.val y.val) ^ Œ±`.

If the original space is a (pseudo) extended metric space, then so is `WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ`.
-/

section EDist

variable [EDist X]

instance : EDist (WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) where
  edist x y := edist x.val y.val ^ Œ±

theorem edist_def (x y : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) : edist x y = edist x.val y.val ^ Œ± := rfl

@[simp]
theorem edist_mk_mk (x y : X) : edist (mk x : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) (mk y) = edist x y ^ Œ± :=
  rfl

@[simp]
theorem edist_val_val (x y : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) : edist x.val y.val = edist x y ^ Œ±‚Åª¬π := by
  rw [edist_def, ENNReal.rpow_rpow_inv hŒ±‚ÇÄ.ne']

end EDist

section PseudoEMetricSpace

variable [PseudoEMetricSpace X]

open EMetric

instance : PseudoEMetricSpace (WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) where
  edist_self x := by simp [edist_def, hŒ±‚ÇÄ]
  edist_comm x y := by rw [edist_def, edist_def, edist_comm]
  edist_triangle x y z := by
    simp only [edist_def]
    grw [edist_triangle x.val y.val z.val, ENNReal.rpow_add_le_add_rpow _ _ hŒ±‚ÇÄ.le hŒ±‚ÇÅ]
  toUniformSpace := inferInstance
  uniformity_edist := by
    have H : (ùì§ X).HasBasis (0 < ¬∑) fun x => {p | edist p.1 p.2 < x ^ (Œ±‚Åª¬π)} := by
      refine EMetric.mk_uniformity_basis (fun _ _ ‚Ü¶ by positivity) fun Œµ hŒµ ‚Ü¶
        ‚ü®Œµ ^ Œ±, by positivity, ?_‚ü©
      rw [ENNReal.rpow_rpow_inv hŒ±‚ÇÄ.ne']
    simp (disch := positivity) [uniformity_comap, H.eq_biInf, ENNReal.rpow_lt_rpow_iff]

@[simp]
theorem preimage_val_emetricBall (x : X) (r : ‚Ñù‚â•0‚àû) :
    val ‚Åª¬π' ball x r = ball (mk x : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) (r ^ Œ±) := by
  ext ‚ü®y‚ü©
  simp (disch := positivity) [ENNReal.rpow_lt_rpow_iff]

@[simp]
theorem image_mk_emetricBall (x : X) (r : ‚Ñù‚â•0‚àû) :
    mk '' ball x r = ball (mk x : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) (r ^ Œ±) := by
  rw [image_mk_eq_preimage, preimage_val_emetricBall]

@[simp]
theorem preimage_mk_emetricBall (x : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) (d : ‚Ñù‚â•0‚àû) :
    mk ‚Åª¬π' ball x d = ball x.val (d ^ Œ±‚Åª¬π) := by
  apply injective_mk.image_injective
  rw [image_mk_emetricBall, Set.image_preimage_eq _ surjective_mk, ENNReal.rpow_inv_rpow hŒ±‚ÇÄ.ne']

@[simp]
theorem image_val_emetricBall (x : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) (d : ‚Ñù‚â•0‚àû) :
    val '' ball x d = ball x.val (d ^ Œ±‚Åª¬π) := by
  rw [image_val_eq_preimage, preimage_mk_emetricBall]

@[simp]
theorem preimage_val_emetricClosedBall (x : X) (r : ‚Ñù‚â•0‚àû) :
    val ‚Åª¬π' closedBall x r = closedBall (mk x : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) (r ^ Œ±) := by
  ext ‚ü®y‚ü©
  simp (disch := positivity) [ENNReal.rpow_le_rpow_iff]

@[simp]
theorem image_mk_emetricClosedBall (x : X) (r : ‚Ñù‚â•0‚àû) :
    mk '' closedBall x r = closedBall (mk x : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) (r ^ Œ±) := by
  rw [image_mk_eq_preimage, preimage_val_emetricClosedBall]

@[simp]
theorem preimage_mk_emetricClosedBall (x : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) (d : ‚Ñù‚â•0‚àû) :
    mk ‚Åª¬π' closedBall x d = closedBall x.val (d ^ Œ±‚Åª¬π) := by
  apply injective_mk.image_injective
  rw [image_mk_emetricClosedBall, Set.image_preimage_eq _ surjective_mk,
    ENNReal.rpow_inv_rpow hŒ±‚ÇÄ.ne']

@[simp]
theorem image_val_emetricClosedBall (x : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) (d : ‚Ñù‚â•0‚àû) :
    val '' closedBall x d = closedBall x.val (d ^ Œ±‚Åª¬π) := by
  rw [image_val_eq_preimage, preimage_mk_emetricClosedBall]

@[simp]
theorem ediam_image_val (s : Set (WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ)) : diam (val '' s) = diam s ^ Œ±‚Åª¬π := by
  refine eq_of_forall_ge_iff fun c ‚Ü¶ ?_
  simp [diam_le_iff, ENNReal.rpow_inv_le_iff hŒ±‚ÇÄ]

@[simp]
theorem ediam_preimage_mk (s : Set (WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ)) :
    diam (mk ‚Åª¬π' s) = diam s ^ Œ±‚Åª¬π := by
  rw [‚Üê image_val_eq_preimage, ediam_image_val]

@[simp]
theorem ediam_preimage_val (s : Set X) :
    diam (val ‚Åª¬π' s : Set (WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ)) = diam s ^ Œ± := by
  rw [‚Üê ENNReal.rpow_inv_rpow hŒ±‚ÇÄ.ne' (diam _), ‚Üê ediam_preimage_mk,
    ‚Üê Set.preimage_comp, val_comp_mk, Set.preimage_id]

@[simp]
theorem ediam_image_mk (s : Set X) :
    diam (mk '' s : Set (WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ)) = diam s ^ Œ± := by
  simp [image_mk_eq_preimage]

end PseudoEMetricSpace

instance [EMetricSpace X] : EMetricSpace (WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) :=
  .ofT0PseudoEMetricSpace _

/-!
### Distance and a (pseudo) metric space structure

Th extended distance on `WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ`
is given by `dist x y = (dist x.val y.val) ^ Œ±`.

If the original space is a (pseudo) metric space, then so is `WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ`.
-/

instance [Dist X] : Dist (WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) where
  dist x y := dist x.val y.val ^ Œ±

@[simp]
theorem dist_mk_mk [Dist X] (x y : X) :
    dist (mk x : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) (mk y) = dist x y ^ Œ± :=
  rfl

section PseudoMetricSpace

variable [PseudoMetricSpace X]

instance : PseudoMetricSpace (WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) :=
  letI aux : PseudoMetricSpace (WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) :=
    PseudoEMetricSpace.toPseudoMetricSpaceOfDist dist
      (by rintro ‚ü®x‚ü© ‚ü®y‚ü©; rw [dist_mk_mk]; positivity)
      (by
        rintro ‚ü®x‚ü© ‚ü®y‚ü©
        rw [edist_mk_mk, dist_mk_mk, ‚Üê ENNReal.ofReal_rpow_of_nonneg, ‚Üê edist_dist] <;> positivity)
  aux.replaceBornology fun s ‚Ü¶ by
    rw [‚Üê isBounded_preimage_mk_iff, Metric.isBounded_iff, Metric.isBounded_iff]
    constructor
    ¬∑ rintro ‚ü®C, hC‚ü©
      use C ^ Œ±
      rintro ‚ü®x‚ü© hx ‚ü®y‚ü© hy
      grw [dist_mk_mk, hC hx hy]
    ¬∑ rintro ‚ü®C, hC‚ü©
      use C ^ Œ±‚Åª¬π
      intro x hx y hy
      grw [‚Üê hC hx hy, dist_mk_mk, Real.rpow_rpow_inv (by positivity) hŒ±‚ÇÄ.ne']

open Metric

@[simp]
theorem dist_val_val (x y : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) : dist x.val y.val = dist x y ^ Œ±‚Åª¬π := by
  cases x; cases y
  rw [dist_mk_mk, Real.rpow_rpow_inv dist_nonneg hŒ±‚ÇÄ.ne']

@[simp]
theorem preimage_val_ball (x : X) {r : ‚Ñù} (hr : 0 ‚â§ r) :
    val ‚Åª¬π' ball x r = ball (mk x : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) (r ^ Œ±) := by
  ext ‚ü®y‚ü©
  simp (disch := positivity) [Real.rpow_lt_rpow_iff]

@[simp]
theorem image_mk_ball (x : X) {r : ‚Ñù} (hr : 0 ‚â§ r) :
    mk '' ball x r = ball (mk x : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) (r ^ Œ±) := by
  rw [image_mk_eq_preimage, preimage_val_ball x hr]

@[simp]
theorem preimage_mk_ball (x : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) {r : ‚Ñù} (hr : 0 ‚â§ r) :
    mk ‚Åª¬π' ball x r = ball x.val (r ^ Œ±‚Åª¬π) := by
  apply injective_mk.image_injective
  rw [image_mk_ball _ (by positivity), Set.image_preimage_eq _ surjective_mk,
    Real.rpow_inv_rpow hr hŒ±‚ÇÄ.ne']

@[simp]
theorem image_val_ball (x : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) {r : ‚Ñù} (hr : 0 ‚â§ r) :
    val '' ball x r = ball x.val (r ^ Œ±‚Åª¬π) := by
  rw [image_val_eq_preimage, preimage_mk_ball _ hr]

@[simp]
theorem preimage_val_closedBall (x : X) {r : ‚Ñù} (hr : 0 ‚â§ r) :
    val ‚Åª¬π' closedBall x r = closedBall (mk x : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) (r ^ Œ±) := by
  ext ‚ü®y‚ü©
  simp (disch := positivity) [Real.rpow_le_rpow_iff]

@[simp]
theorem image_mk_closedBall (x : X) {r : ‚Ñù} (hr : 0 ‚â§ r) :
    mk '' closedBall x r = closedBall (mk x : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) (r ^ Œ±) := by
  rw [image_mk_eq_preimage, preimage_val_closedBall x hr]

@[simp]
theorem preimage_mk_closedBall (x : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) {r : ‚Ñù} (hr : 0 ‚â§ r) :
    mk ‚Åª¬π' closedBall x r = closedBall x.val (r ^ Œ±‚Åª¬π) := by
  apply injective_mk.image_injective
  rw [image_mk_closedBall _ (by positivity), Set.image_preimage_eq _ surjective_mk,
    Real.rpow_inv_rpow hr hŒ±‚ÇÄ.ne']

@[simp]
theorem image_val_closedBall (x : WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) {r : ‚Ñù} (hr : 0 ‚â§ r) :
    val '' closedBall x r = closedBall x.val (r ^ Œ±‚Åª¬π) := by
  rw [image_val_eq_preimage, preimage_mk_closedBall _ hr]

end PseudoMetricSpace

instance [MetricSpace X] : MetricSpace (WithRPowDist X Œ± hŒ±‚ÇÄ hŒ±‚ÇÅ) :=
  .ofT0PseudoMetricSpace _

end WithRPowDist
