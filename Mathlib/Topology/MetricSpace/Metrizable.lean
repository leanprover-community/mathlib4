/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.SpecificLimits.Basic
import Mathlib.Topology.UrysohnsLemma
import Mathlib.Topology.ContinuousFunction.Bounded
import Mathlib.Topology.UniformSpace.Cauchy

#align_import topology.metric_space.metrizable from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Metrizability of a Tâ‚ƒ topological space with second countable topology

In this file we define metrizable topological spaces, i.e., topological spaces for which there
exists a metric space structure that generates the same topology.

We also show that a Tâ‚ƒ topological space with second countable topology `X` is metrizable.

First we prove that `X` can be embedded into `l^âˆž`, then use this embedding to pull back the metric
space structure.
-/


open Set Filter Metric

open BoundedContinuousFunction Filter Topology

namespace TopologicalSpace

variable {Î¹ X Y : Type*} {Ï€ : Î¹ â†’ Type*} [TopologicalSpace X] [TopologicalSpace Y] [Finite Î¹]
  [âˆ€ i, TopologicalSpace (Ï€ i)]

/-- A topological space is *pseudo metrizable* if there exists a pseudo metric space structure
compatible with the topology. To endow such a space with a compatible distance, use
`letI : PseudoMetricSpace X := TopologicalSpace.pseudoMetrizableSpacePseudoMetric X`. -/
class PseudoMetrizableSpace (X : Type*) [t : TopologicalSpace X] : Prop where
  exists_pseudo_metric : âˆƒ m : PseudoMetricSpace X, m.toUniformSpace.toTopologicalSpace = t
#align topological_space.pseudo_metrizable_space TopologicalSpace.PseudoMetrizableSpace

instance (priority := 100) _root_.PseudoMetricSpace.toPseudoMetrizableSpace {X : Type*}
    [m : PseudoMetricSpace X] : PseudoMetrizableSpace X :=
  âŸ¨âŸ¨m, rflâŸ©âŸ©
#align pseudo_metric_space.to_pseudo_metrizable_space PseudoMetricSpace.toPseudoMetrizableSpace

/-- Construct on a metrizable space a metric compatible with the topology. -/
noncomputable def pseudoMetrizableSpacePseudoMetric (X : Type*) [TopologicalSpace X]
    [h : PseudoMetrizableSpace X] : PseudoMetricSpace X :=
  h.exists_pseudo_metric.choose.replaceTopology h.exists_pseudo_metric.choose_spec.symm
#align topological_space.pseudo_metrizable_space_pseudo_metric TopologicalSpace.pseudoMetrizableSpacePseudoMetric

instance pseudoMetrizableSpace_prod [PseudoMetrizableSpace X] [PseudoMetrizableSpace Y] :
    PseudoMetrizableSpace (X Ã— Y) :=
  letI : PseudoMetricSpace X := pseudoMetrizableSpacePseudoMetric X
  letI : PseudoMetricSpace Y := pseudoMetrizableSpacePseudoMetric Y
  inferInstance
#align topological_space.pseudo_metrizable_space_prod TopologicalSpace.pseudoMetrizableSpace_prod

/-- Given an inducing map of a topological space into a pseudo metrizable space, the source space
is also pseudo metrizable. -/
theorem _root_.Inducing.pseudoMetrizableSpace [PseudoMetrizableSpace Y] {f : X â†’ Y}
    (hf : Inducing f) : PseudoMetrizableSpace X :=
  letI : PseudoMetricSpace Y := pseudoMetrizableSpacePseudoMetric Y
  âŸ¨âŸ¨hf.comapPseudoMetricSpace, rflâŸ©âŸ©
#align inducing.pseudo_metrizable_space Inducing.pseudoMetrizableSpace

/-- Every pseudo-metrizable space is first countable. -/
instance (priority := 100) PseudoMetrizableSpace.firstCountableTopology
    [h : PseudoMetrizableSpace X] : TopologicalSpace.FirstCountableTopology X := by
  rcases h with âŸ¨_, hmâŸ©
  -- âŠ¢ FirstCountableTopology X
  rw [â† hm]
  -- âŠ¢ FirstCountableTopology X
  exact @UniformSpace.firstCountableTopology X PseudoMetricSpace.toUniformSpace
    EMetric.instIsCountablyGeneratedUniformity
#align topological_space.pseudo_metrizable_space.first_countable_topology TopologicalSpace.PseudoMetrizableSpace.firstCountableTopology

instance PseudoMetrizableSpace.subtype [PseudoMetrizableSpace X] (s : Set X) :
    PseudoMetrizableSpace s :=
  inducing_subtype_val.pseudoMetrizableSpace
#align topological_space.pseudo_metrizable_space.subtype TopologicalSpace.PseudoMetrizableSpace.subtype

instance pseudoMetrizableSpace_pi [âˆ€ i, PseudoMetrizableSpace (Ï€ i)] :
    PseudoMetrizableSpace (âˆ€ i, Ï€ i) := by
  cases nonempty_fintype Î¹
  -- âŠ¢ PseudoMetrizableSpace ((i : Î¹) â†’ Ï€ i)
  letI := fun i => pseudoMetrizableSpacePseudoMetric (Ï€ i)
  -- âŠ¢ PseudoMetrizableSpace ((i : Î¹) â†’ Ï€ i)
  infer_instance
  -- ðŸŽ‰ no goals
#align topological_space.pseudo_metrizable_space_pi TopologicalSpace.pseudoMetrizableSpace_pi

/-- A topological space is metrizable if there exists a metric space structure compatible with the
topology. To endow such a space with a compatible distance, use
`letI : MetricSpace X := TopologicalSpace.metrizableSpaceMetric X`. -/
class MetrizableSpace (X : Type*) [t : TopologicalSpace X] : Prop where
  exists_metric : âˆƒ m : MetricSpace X, m.toUniformSpace.toTopologicalSpace = t
#align topological_space.metrizable_space TopologicalSpace.MetrizableSpace

instance (priority := 100) _root_.MetricSpace.toMetrizableSpace {X : Type*} [m : MetricSpace X] :
    MetrizableSpace X :=
  âŸ¨âŸ¨m, rflâŸ©âŸ©
#align metric_space.to_metrizable_space MetricSpace.toMetrizableSpace

instance (priority := 100) MetrizableSpace.toPseudoMetrizableSpace [h : MetrizableSpace X] :
    PseudoMetrizableSpace X :=
  let âŸ¨m, hmâŸ© := h.1
  âŸ¨âŸ¨m.toPseudoMetricSpace, hmâŸ©âŸ©
#align topological_space.metrizable_space.to_pseudo_metrizable_space TopologicalSpace.MetrizableSpace.toPseudoMetrizableSpace

/-- Construct on a metrizable space a metric compatible with the topology. -/
noncomputable def metrizableSpaceMetric (X : Type*) [TopologicalSpace X] [h : MetrizableSpace X] :
    MetricSpace X :=
  h.exists_metric.choose.replaceTopology h.exists_metric.choose_spec.symm
#align topological_space.metrizable_space_metric TopologicalSpace.metrizableSpaceMetric

instance (priority := 100) t2Space_of_metrizableSpace [MetrizableSpace X] : T2Space X :=
  letI : MetricSpace X := metrizableSpaceMetric X
  inferInstance
#align topological_space.t2_space_of_metrizable_space TopologicalSpace.t2Space_of_metrizableSpace

instance metrizableSpace_prod [MetrizableSpace X] [MetrizableSpace Y] : MetrizableSpace (X Ã— Y) :=
  letI : MetricSpace X := metrizableSpaceMetric X
  letI : MetricSpace Y := metrizableSpaceMetric Y
  inferInstance
#align topological_space.metrizable_space_prod TopologicalSpace.metrizableSpace_prod

/-- Given an embedding of a topological space into a metrizable space, the source space is also
metrizable. -/
theorem _root_.Embedding.metrizableSpace [MetrizableSpace Y] {f : X â†’ Y} (hf : Embedding f) :
    MetrizableSpace X :=
  letI : MetricSpace Y := metrizableSpaceMetric Y
  âŸ¨âŸ¨hf.comapMetricSpace f, rflâŸ©âŸ©
#align embedding.metrizable_space Embedding.metrizableSpace

instance MetrizableSpace.subtype [MetrizableSpace X] (s : Set X) : MetrizableSpace s :=
  embedding_subtype_val.metrizableSpace
#align topological_space.metrizable_space.subtype TopologicalSpace.MetrizableSpace.subtype

instance metrizableSpace_pi [âˆ€ i, MetrizableSpace (Ï€ i)] : MetrizableSpace (âˆ€ i, Ï€ i) := by
  cases nonempty_fintype Î¹
  -- âŠ¢ MetrizableSpace ((i : Î¹) â†’ Ï€ i)
  letI := fun i => metrizableSpaceMetric (Ï€ i)
  -- âŠ¢ MetrizableSpace ((i : Î¹) â†’ Ï€ i)
  infer_instance
  -- ðŸŽ‰ no goals
#align topological_space.metrizable_space_pi TopologicalSpace.metrizableSpace_pi

theorem IsSeparable.secondCountableTopology [PseudoMetrizableSpace X] {s : Set X}
    (hs : IsSeparable s) : SecondCountableTopology s := by
  letI := pseudoMetrizableSpacePseudoMetric X
  -- âŠ¢ SecondCountableTopology â†‘s
  have := hs.separableSpace
  -- âŠ¢ SecondCountableTopology â†‘s
  exact UniformSpace.secondCountable_of_separable s
  -- ðŸŽ‰ no goals

variable (X)
variable [T3Space X] [SecondCountableTopology X]

/-- A Tâ‚ƒ topological space with second countable topology can be embedded into `l^âˆž = â„• â†’áµ‡ â„`. -/
theorem exists_embedding_l_infty : âˆƒ f : X â†’ â„• â†’áµ‡ â„, Embedding f := by
  haveI : NormalSpace X := normalSpaceOfT3SecondCountable X
  -- âŠ¢ âˆƒ f, Embedding f
  -- Choose a countable basis, and consider the set `s` of pairs of set `(U, V)` such that `U âˆˆ B`,
  -- `V âˆˆ B`, and `closure U âŠ† V`.
  rcases exists_countable_basis X with âŸ¨B, hBc, -, hBâŸ©
  -- âŠ¢ âˆƒ f, Embedding f
  let s : Set (Set X Ã— Set X) := { UV âˆˆ B Ã—Ë¢ B | closure UV.1 âŠ† UV.2 }
  -- âŠ¢ âˆƒ f, Embedding f
  -- `s` is a countable set.
  haveI : Encodable s := ((hBc.prod hBc).mono (inter_subset_left _ _)).toEncodable
  -- âŠ¢ âˆƒ f, Embedding f
  -- We don't have the space of bounded (possibly discontinuous) functions, so we equip `s`
  -- with the discrete topology and deal with `s â†’áµ‡ â„` instead.
  letI : TopologicalSpace s := âŠ¥
  -- âŠ¢ âˆƒ f, Embedding f
  haveI : DiscreteTopology s := âŸ¨rflâŸ©
  -- âŠ¢ âˆƒ f, Embedding f
  rsuffices âŸ¨f, hfâŸ© : âˆƒ f : X â†’ s â†’áµ‡ â„, Embedding f
  -- âŠ¢ âˆƒ f, Embedding f
  Â· exact âŸ¨fun x => (f x).extend (Encodable.encode' s) 0,
      (BoundedContinuousFunction.isometry_extend (Encodable.encode' s) (0 : â„• â†’áµ‡ â„)).embedding.comp
        hfâŸ©
  have hd : âˆ€ UV : s, Disjoint (closure UV.1.1) UV.1.2á¶œ :=
    fun UV => disjoint_compl_right.mono_right (compl_subset_compl.2 UV.2.2)
  -- Choose a sequence of `Îµâ‚™ > 0`, `n : s`, that is bounded above by `1` and tends to zero
  -- along the `cofinite` filter.
  obtain âŸ¨Îµ, Îµ01, hÎµâŸ© : âˆƒ Îµ : s â†’ â„, (âˆ€ UV, Îµ UV âˆˆ Ioc (0 : â„) 1) âˆ§ Tendsto Îµ cofinite (ð“ 0) := by
    rcases posSumOfEncodable zero_lt_one s with âŸ¨Îµ, Îµ0, c, hÎµc, hc1âŸ©
    refine' âŸ¨Îµ, fun UV => âŸ¨Îµ0 UV, _âŸ©, hÎµc.summable.tendsto_cofinite_zeroâŸ©
    exact (le_hasSum hÎµc UV fun _ _ => (Îµ0 _).le).trans hc1
  /- For each `UV = (U, V) âˆˆ s` we use Urysohn's lemma to choose a function `f UV` that is equal to
    zero on `U` and is equal to `Îµ UV` on the complement to `V`. -/
  have : âˆ€ UV : s, âˆƒ f : C(X, â„),
      EqOn f 0 UV.1.1 âˆ§ EqOn f (fun _ => Îµ UV) UV.1.2á¶œ âˆ§ âˆ€ x, f x âˆˆ Icc 0 (Îµ UV) := by
    intro UV
    rcases exists_continuous_zero_one_of_closed isClosed_closure
        (hB.isOpen UV.2.1.2).isClosed_compl (hd UV) with
      âŸ¨f, hfâ‚€, hfâ‚, hf01âŸ©
    exact âŸ¨Îµ UV â€¢ f, fun x hx => by simp [hfâ‚€ (subset_closure hx)], fun x hx => by simp [hfâ‚ hx],
      fun x => âŸ¨mul_nonneg (Îµ01 _).1.le (hf01 _).1, mul_le_of_le_one_right (Îµ01 _).1.le (hf01 _).2âŸ©âŸ©
  choose f hf0 hfÎµ hf0Îµ using this
  -- âŠ¢ âˆƒ f, Embedding f
  have hf01 : âˆ€ UV x, f UV x âˆˆ Icc (0 : â„) 1 :=
    fun UV x => Icc_subset_Icc_right (Îµ01 _).2 (hf0Îµ _ _)
  -- The embedding is given by `F x UV = f UV x`.
  set F : X â†’ s â†’áµ‡ â„ := fun x =>
    âŸ¨âŸ¨fun UV => f UV x, continuous_of_discreteTopologyâŸ©, 1,
      fun UVâ‚ UVâ‚‚ => Real.dist_le_of_mem_Icc_01 (hf01 _ _) (hf01 _ _)âŸ©
  have hF : âˆ€ x UV, F x UV = f UV x := fun _ _ => rfl
  -- âŠ¢ âˆƒ f, Embedding f
  refine' âŸ¨F, Embedding.mk' _ (fun x y hxy => _) fun x => le_antisymm _ _âŸ©
  Â· /- First we prove that `F` is injective. Indeed, if `F x = F y` and `x â‰  y`, then we can find
    `(U, V) âˆˆ s` such that `x âˆˆ U` and `y âˆ‰ V`, hence `F x UV = 0 â‰  Îµ UV = F y UV`. -/
    by_contra Hne
    -- âŠ¢ False
    rcases hB.mem_nhds_iff.1 (isOpen_ne.mem_nhds Hne) with âŸ¨V, hVB, hxV, hVyâŸ©
    -- âŠ¢ False
    rcases hB.exists_closure_subset (hB.mem_nhds hVB hxV) with âŸ¨U, hUB, hxU, hUVâŸ©
    -- âŠ¢ False
    set UV : â†¥s := âŸ¨(U, V), âŸ¨hUB, hVBâŸ©, hUVâŸ©
    -- âŠ¢ False
    apply (Îµ01 UV).1.ne
    -- âŠ¢ 0 = Îµ UV
    calc
      (0 : â„) = F x UV := (hf0 UV hxU).symm
      _ = F y UV := by rw [hxy]
      _ = Îµ UV := hfÎµ UV fun h : y âˆˆ V => hVy h rfl
  Â· /- Now we prove that each neighborhood `V` of `x : X` include a preimage of a neighborhood of
    `F x` under `F`. Without loss of generality, `V` belongs to `B`. Choose `U âˆˆ B` such that
    `x âˆˆ V` and `closure V âŠ† U`. Then the preimage of the `(Îµ (U, V))`-neighborhood of `F x`
    is included by `V`. -/
    refine' ((nhds_basis_ball.comap _).le_basis_iff hB.nhds_hasBasis).2 _
    -- âŠ¢ âˆ€ (i' : Set X), i' âˆˆ B âˆ§ x âˆˆ i' â†’ âˆƒ i, 0 < i âˆ§ F â»Â¹' ball (F x) i âŠ† i'
    rintro V âŸ¨hVB, hxVâŸ©
    -- âŠ¢ âˆƒ i, 0 < i âˆ§ F â»Â¹' ball (F x) i âŠ† V
    rcases hB.exists_closure_subset (hB.mem_nhds hVB hxV) with âŸ¨U, hUB, hxU, hUVâŸ©
    -- âŠ¢ âˆƒ i, 0 < i âˆ§ F â»Â¹' ball (F x) i âŠ† V
    set UV : â†¥s := âŸ¨(U, V), âŸ¨hUB, hVBâŸ©, hUVâŸ©
    -- âŠ¢ âˆƒ i, 0 < i âˆ§ F â»Â¹' ball (F x) i âŠ† V
    refine' âŸ¨Îµ UV, (Îµ01 UV).1, fun y (hy : dist (F y) (F x) < Îµ UV) => _âŸ©
    -- âŠ¢ y âˆˆ V
    replace hy : dist (F y UV) (F x UV) < Îµ UV
    -- âŠ¢ dist (â†‘(F y) UV) (â†‘(F x) UV) < Îµ UV
    exact (BoundedContinuousFunction.dist_coe_le_dist _).trans_lt hy
    -- âŠ¢ y âˆˆ V
    contrapose! hy
    -- âŠ¢ Îµ { val := (U, V), property := (_ : (U, V) âˆˆ B Ã—Ë¢ B âˆ§ closure (U, V).fst âŠ† ( â€¦
    rw [hF, hF, hfÎµ UV hy, hf0 UV hxU, Pi.zero_apply, dist_zero_right]
    -- âŠ¢ Îµ { val := (U, V), property := (_ : (U, V) âˆˆ B Ã—Ë¢ B âˆ§ closure (U, V).fst âŠ† ( â€¦
    exact le_abs_self _
    -- ðŸŽ‰ no goals
  Â· /- Finally, we prove that `F` is continuous. Given `Î´ > 0`, consider the set `T` of `(U, V) âˆˆ s`
    such that `Îµ (U, V) â‰¥ Î´`. Since `Îµ` tends to zero, `T` is finite. Since each `f` is continuous,
    we can choose a neighborhood such that `dist (F y (U, V)) (F x (U, V)) â‰¤ Î´` for any
    `(U, V) âˆˆ T`. For `(U, V) âˆ‰ T`, the same inequality is true because both `F y (U, V)` and
    `F x (U, V)` belong to the interval `[0, Îµ (U, V)]`. -/
    refine' (nhds_basis_closedBall.comap _).ge_iff.2 fun Î´ Î´0 => _
    -- âŠ¢ F â»Â¹' closedBall (F x) Î´ âˆˆ ð“ x
    have h_fin : { UV : s | Î´ â‰¤ Îµ UV }.Finite := by simpa only [â† not_lt] using hÎµ (gt_mem_nhds Î´0)
    -- âŠ¢ F â»Â¹' closedBall (F x) Î´ âˆˆ ð“ x
    have : âˆ€á¶  y in ð“ x, âˆ€ UV, Î´ â‰¤ Îµ UV â†’ dist (F y UV) (F x UV) â‰¤ Î´ := by
      refine' (eventually_all_finite h_fin).2 fun UV _ => _
      exact (f UV).continuous.tendsto x (closedBall_mem_nhds _ Î´0)
    refine' this.mono fun y hy => (BoundedContinuousFunction.dist_le Î´0.le).2 fun UV => _
    -- âŠ¢ dist (â†‘(F y) UV) (â†‘(F x) UV) â‰¤ Î´
    cases' le_total Î´ (Îµ UV) with hle hle
    -- âŠ¢ dist (â†‘(F y) UV) (â†‘(F x) UV) â‰¤ Î´
    exacts [hy _ hle, (Real.dist_le_of_mem_Icc (hf0Îµ _ _) (hf0Îµ _ _)).trans (by rwa [sub_zero])]
    -- ðŸŽ‰ no goals
#align topological_space.exists_embedding_l_infty TopologicalSpace.exists_embedding_l_infty

/-- *Urysohn's metrization theorem* (Tychonoff's version): a Tâ‚ƒ topological space with second
countable topology `X` is metrizable, i.e., there exists a metric space structure that generates the
same topology. -/
theorem metrizableSpace_of_t3_second_countable : MetrizableSpace X :=
  let âŸ¨_, hfâŸ© := exists_embedding_l_infty X
  hf.metrizableSpace
#align topological_space.metrizable_space_of_t3_second_countable TopologicalSpace.metrizableSpace_of_t3_second_countable

instance : MetrizableSpace ENNReal :=
  metrizableSpace_of_t3_second_countable ENNReal

end TopologicalSpace
