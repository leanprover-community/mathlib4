/-
Copyright (c) 2022 Hanting Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Hanting Zhang
-/
import Mathlib.Topology.MetricSpace.Antilipschitz
import Mathlib.Topology.MetricSpace.Isometry
import Mathlib.Topology.MetricSpace.Lipschitz
import Mathlib.Data.FunLike.Basic

/-!
# Dilations

We define dilations, i.e., maps between emetric spaces that satisfy
`edist (f x) (f y) = r * edist x y` for some `r ‚àâ {0, ‚àû}`.

The value `r = 0` is not allowed because we want dilations of (e)metric spaces to be automatically
injective. The value `r = ‚àû` is not allowed because this way we can define `Dilation.ratio f : ‚Ñù‚â•0`,
not `Dilation.ratio f : ‚Ñù‚â•0‚àû`. Also, we do not often need maps sending distinct points to points at
infinite distance.

## Main definitions

* `Dilation.ratio f : ‚Ñù‚â•0`: the value of `r` in the relation above, defaulting to 1 in the case
  where it is not well-defined.

## Notation

- `Œ± ‚Üí·µà Œ≤`: notation for `Dilation Œ± Œ≤`.

## Implementation notes

The type of dilations defined in this file are also referred to as "similarities" or "similitudes"
by other authors. The name `Dilation` was chosen to match the Wikipedia name.

Since a lot of elementary properties don't require `eq_of_dist_eq_zero` we start setting up the
theory for `PseudoEMetricSpace` and we specialize to `PseudoMetricSpace` and `MetricSpace` when
needed.

## TODO

- Introduce dilation equivs.
- Refactor the `Isometry` API to match the `*HomClass` API below.

## References

- https://en.wikipedia.org/wiki/Dilation_(metric_space)
- [Marcel Berger, *Geometry*][berger1987]
-/

noncomputable section

open Bornology Function Set Topology
open scoped ENNReal NNReal

section Defs

variable (Œ± : Type*) (Œ≤ : Type*) [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤]

/-- A dilation is a map that uniformly scales the edistance between any two points. -/
structure Dilation where
  toFun : Œ± ‚Üí Œ≤
  edist_eq' : ‚àÉ r : ‚Ñù‚â•0, r ‚â† 0 ‚àß ‚àÄ x y : Œ±, edist (toFun x) (toFun y) = r * edist x y

@[inherit_doc] infixl:25 " ‚Üí·µà " => Dilation

/-- `DilationClass F Œ± Œ≤ r` states that `F` is a type of `r`-dilations.
You should extend this typeclass when you extend `Dilation`. -/
class DilationClass (F : Type*) (Œ± Œ≤ : outParam Type*) [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤]
    [FunLike F Œ± Œ≤] : Prop where
  edist_eq' : ‚àÄ f : F, ‚àÉ r : ‚Ñù‚â•0, r ‚â† 0 ‚àß ‚àÄ x y : Œ±, edist (f x) (f y) = r * edist x y

end Defs

namespace Dilation

variable {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*} {F : Type*}

section Setup

variable [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤]

instance funLike : FunLike (Œ± ‚Üí·µà Œ≤) Œ± Œ≤ where
  coe := toFun
  coe_injective' f g h := by cases f; cases g; congr

instance toDilationClass : DilationClass (Œ± ‚Üí·µà Œ≤) Œ± Œ≤ where
  edist_eq' f := edist_eq' f

@[simp]
theorem toFun_eq_coe {f : Œ± ‚Üí·µà Œ≤} : f.toFun = (f : Œ± ‚Üí Œ≤) :=
  rfl

@[simp]
theorem coe_mk (f : Œ± ‚Üí Œ≤) (h) : ‚áë(‚ü®f, h‚ü© : Œ± ‚Üí·µà Œ≤) = f :=
  rfl

protected theorem congr_fun {f g : Œ± ‚Üí·µà Œ≤} (h : f = g) (x : Œ±) : f x = g x :=
  DFunLike.congr_fun h x

protected theorem congr_arg (f : Œ± ‚Üí·µà Œ≤) {x y : Œ±} (h : x = y) : f x = f y :=
  DFunLike.congr_arg f h

@[ext]
theorem ext {f g : Œ± ‚Üí·µà Œ≤} (h : ‚àÄ x, f x = g x) : f = g :=
  DFunLike.ext f g h

@[simp]
theorem mk_coe (f : Œ± ‚Üí·µà Œ≤) (h) : Dilation.mk f h = f :=
  ext fun _ => rfl

/-- Copy of a `Dilation` with a new `toFun` equal to the old one. Useful to fix definitional
equalities. -/
@[simps -fullyApplied]
protected def copy (f : Œ± ‚Üí·µà Œ≤) (f' : Œ± ‚Üí Œ≤) (h : f' = ‚áëf) : Œ± ‚Üí·µà Œ≤ where
  toFun := f'
  edist_eq' := h.symm ‚ñ∏ f.edist_eq'

theorem copy_eq_self (f : Œ± ‚Üí·µà Œ≤) {f' : Œ± ‚Üí Œ≤} (h : f' = f) : f.copy f' h = f :=
  DFunLike.ext' h

variable [FunLike F Œ± Œ≤]

open Classical in
/-- The ratio of a dilation `f`. If the ratio is undefined (i.e., the distance between any two
points in `Œ±` is either zero or infinity), then we choose one as the ratio. -/
def ratio [DilationClass F Œ± Œ≤] (f : F) : ‚Ñù‚â•0 :=
  if ‚àÄ x y : Œ±, edist x y = 0 ‚à® edist x y = ‚ä§ then 1 else (DilationClass.edist_eq' f).choose

theorem ratio_of_trivial [DilationClass F Œ± Œ≤] (f : F)
    (h : ‚àÄ x y : Œ±, edist x y = 0 ‚à® edist x y = ‚àû) : ratio f = 1 :=
  if_pos h

@[nontriviality]
theorem ratio_of_subsingleton [Subsingleton Œ±] [DilationClass F Œ± Œ≤] (f : F) : ratio f = 1 :=
  if_pos fun x y ‚Ü¶ by simp [Subsingleton.elim x y]

theorem ratio_ne_zero [DilationClass F Œ± Œ≤] (f : F) : ratio f ‚â† 0 := by
  rw [ratio]; split_ifs
  ¬∑ exact one_ne_zero
  exact (DilationClass.edist_eq' f).choose_spec.1

theorem ratio_pos [DilationClass F Œ± Œ≤] (f : F) : 0 < ratio f :=
  (ratio_ne_zero f).bot_lt

@[simp]
theorem edist_eq [DilationClass F Œ± Œ≤] (f : F) (x y : Œ±) :
    edist (f x) (f y) = ratio f * edist x y := by
  rw [ratio]; split_ifs with key
  ¬∑ rcases DilationClass.edist_eq' f with ‚ü®r, hne, hr‚ü©
    replace hr := hr x y
    rcases key x y with h | h
    ¬∑ simp only [hr, h, mul_zero]
    ¬∑ simp [hr, h, hne]
  exact (DilationClass.edist_eq' f).choose_spec.2 x y

@[simp]
theorem nndist_eq {Œ± Œ≤ F : Type*} [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] [FunLike F Œ± Œ≤]
    [DilationClass F Œ± Œ≤] (f : F) (x y : Œ±) :
    nndist (f x) (f y) = ratio f * nndist x y := by
  simp only [‚Üê ENNReal.coe_inj, ‚Üê edist_nndist, ENNReal.coe_mul, edist_eq]

@[simp]
theorem dist_eq {Œ± Œ≤ F : Type*} [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] [FunLike F Œ± Œ≤]
    [DilationClass F Œ± Œ≤] (f : F) (x y : Œ±) :
    dist (f x) (f y) = ratio f * dist x y := by
  simp only [dist_nndist, nndist_eq, NNReal.coe_mul]

/-- The `ratio` is equal to the distance ratio for any two points with nonzero finite distance.
`dist` and `nndist` versions below -/
theorem ratio_unique [DilationClass F Œ± Œ≤] {f : F} {x y : Œ±} {r : ‚Ñù‚â•0} (h‚ÇÄ : edist x y ‚â† 0)
    (htop : edist x y ‚â† ‚ä§) (hr : edist (f x) (f y) = r * edist x y) : r = ratio f := by
  simpa only [hr, ENNReal.mul_left_inj h‚ÇÄ htop, ENNReal.coe_inj] using edist_eq f x y

/-- The `ratio` is equal to the distance ratio for any two points
with nonzero finite distance; `nndist` version -/
theorem ratio_unique_of_nndist_ne_zero {Œ± Œ≤ F : Type*} [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤]
    [FunLike F Œ± Œ≤] [DilationClass F Œ± Œ≤] {f : F} {x y : Œ±} {r : ‚Ñù‚â•0} (hxy : nndist x y ‚â† 0)
    (hr : nndist (f x) (f y) = r * nndist x y) : r = ratio f :=
  ratio_unique (by rwa [edist_nndist, ENNReal.coe_ne_zero]) (edist_ne_top x y)
    (by rw [edist_nndist, edist_nndist, hr, ENNReal.coe_mul])

/-- The `ratio` is equal to the distance ratio for any two points
with nonzero finite distance; `dist` version -/
theorem ratio_unique_of_dist_ne_zero {Œ± Œ≤} {F : Type*} [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤]
    [FunLike F Œ± Œ≤] [DilationClass F Œ± Œ≤] {f : F} {x y : Œ±} {r : ‚Ñù‚â•0} (hxy : dist x y ‚â† 0)
    (hr : dist (f x) (f y) = r * dist x y) : r = ratio f :=
  ratio_unique_of_nndist_ne_zero (NNReal.coe_ne_zero.1 hxy) <|
    NNReal.eq <| by rw [coe_nndist, hr, NNReal.coe_mul, coe_nndist]

/-- Alternative `Dilation` constructor when the distance hypothesis is over `nndist` -/
def mkOfNNDistEq {Œ± Œ≤} [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] (f : Œ± ‚Üí Œ≤)
    (h : ‚àÉ r : ‚Ñù‚â•0, r ‚â† 0 ‚àß ‚àÄ x y : Œ±, nndist (f x) (f y) = r * nndist x y) : Œ± ‚Üí·µà Œ≤ where
  toFun := f
  edist_eq' := by
    rcases h with ‚ü®r, hne, h‚ü©
    refine ‚ü®r, hne, fun x y => ?_‚ü©
    rw [edist_nndist, edist_nndist, ‚Üê ENNReal.coe_mul, h x y]

@[simp]
theorem coe_mkOfNNDistEq {Œ± Œ≤} [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] (f : Œ± ‚Üí Œ≤) (h) :
    ‚áë(mkOfNNDistEq f h : Œ± ‚Üí·µà Œ≤) = f :=
  rfl

@[simp]
theorem mk_coe_of_nndist_eq {Œ± Œ≤} [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] (f : Œ± ‚Üí·µà Œ≤)
    (h) : Dilation.mkOfNNDistEq f h = f :=
  ext fun _ => rfl

/-- Alternative `Dilation` constructor when the distance hypothesis is over `dist` -/
def mkOfDistEq {Œ± Œ≤} [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] (f : Œ± ‚Üí Œ≤)
    (h : ‚àÉ r : ‚Ñù‚â•0, r ‚â† 0 ‚àß ‚àÄ x y : Œ±, dist (f x) (f y) = r * dist x y) : Œ± ‚Üí·µà Œ≤ :=
  mkOfNNDistEq f <|
    h.imp fun r hr =>
      ‚ü®hr.1, fun x y => NNReal.eq <| by rw [coe_nndist, hr.2, NNReal.coe_mul, coe_nndist]‚ü©

@[simp]
theorem coe_mkOfDistEq {Œ± Œ≤} [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] (f : Œ± ‚Üí Œ≤) (h) :
    ‚áë(mkOfDistEq f h : Œ± ‚Üí·µà Œ≤) = f :=
  rfl

@[simp]
theorem mk_coe_of_dist_eq {Œ± Œ≤} [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] (f : Œ± ‚Üí·µà Œ≤) (h) :
    Dilation.mkOfDistEq f h = f :=
  ext fun _ => rfl

end Setup

section PseudoEmetricDilation

variable [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] [PseudoEMetricSpace Œ≥]
variable [FunLike F Œ± Œ≤] [DilationClass F Œ± Œ≤]
variable (f : F)

/-- Every isometry is a dilation of ratio `1`. -/
@[simps]
def _root_.Isometry.toDilation (f : Œ± ‚Üí Œ≤) (hf : Isometry f) : Œ± ‚Üí·µà Œ≤ where
  toFun := f
  edist_eq' := ‚ü®1, one_ne_zero, by simpa using hf‚ü©

@[simp]
lemma _root_.Isometry.toDilation_ratio {f : Œ± ‚Üí Œ≤} {hf : Isometry f} : ratio hf.toDilation = 1 := by
  by_cases h : ‚àÄ x y : Œ±, edist x y = 0 ‚à® edist x y = ‚ä§
  ¬∑ exact ratio_of_trivial hf.toDilation h
  ¬∑ push_neg at h
    obtain ‚ü®x, y, h‚ÇÅ, h‚ÇÇ‚ü© := h
    exact ratio_unique h‚ÇÅ h‚ÇÇ (by simp [hf x y]) |>.symm

theorem lipschitz : LipschitzWith (ratio f) (f : Œ± ‚Üí Œ≤) := fun x y => (edist_eq f x y).le

theorem antilipschitz : AntilipschitzWith (ratio f)‚Åª¬π (f : Œ± ‚Üí Œ≤) := fun x y => by
  have hr : ratio f ‚â† 0 := ratio_ne_zero f
  exact mod_cast
    (ENNReal.mul_le_iff_le_inv (ENNReal.coe_ne_zero.2 hr) ENNReal.coe_ne_top).1 (edist_eq f x y).ge

/-- A dilation from an emetric space is injective -/
protected theorem injective {Œ± : Type*} [EMetricSpace Œ±] [FunLike F Œ± Œ≤] [DilationClass F Œ± Œ≤]
    (f : F) :
    Injective f :=
  (antilipschitz f).injective

/-- The identity is a dilation -/
protected def id (Œ±) [PseudoEMetricSpace Œ±] : Œ± ‚Üí·µà Œ± where
  toFun := id
  edist_eq' := ‚ü®1, one_ne_zero, fun x y => by simp only [id, ENNReal.coe_one, one_mul]‚ü©

instance : Inhabited (Œ± ‚Üí·µà Œ±) :=
  ‚ü®Dilation.id Œ±‚ü©

@[simp]
protected theorem coe_id : ‚áë(Dilation.id Œ±) = id :=
  rfl

theorem ratio_id : ratio (Dilation.id Œ±) = 1 := by
  by_cases h : ‚àÄ x y : Œ±, edist x y = 0 ‚à® edist x y = ‚àû
  ¬∑ rw [ratio, if_pos h]
  ¬∑ push_neg at h
    rcases h with ‚ü®x, y, hne‚ü©
    refine (ratio_unique hne.1 hne.2 ?_).symm
    simp

/-- The composition of dilations is a dilation -/
def comp (g : Œ≤ ‚Üí·µà Œ≥) (f : Œ± ‚Üí·µà Œ≤) : Œ± ‚Üí·µà Œ≥ where
  toFun := g ‚àò f
  edist_eq' := ‚ü®ratio g * ratio f, mul_ne_zero (ratio_ne_zero g) (ratio_ne_zero f),
    fun x y => by simp_rw [Function.comp, edist_eq, ENNReal.coe_mul, mul_assoc]‚ü©

theorem comp_assoc {Œ¥ : Type*} [PseudoEMetricSpace Œ¥] (f : Œ± ‚Üí·µà Œ≤) (g : Œ≤ ‚Üí·µà Œ≥)
    (h : Œ≥ ‚Üí·µà Œ¥) : (h.comp g).comp f = h.comp (g.comp f) :=
  rfl

@[simp]
theorem coe_comp (g : Œ≤ ‚Üí·µà Œ≥) (f : Œ± ‚Üí·µà Œ≤) : (g.comp f : Œ± ‚Üí Œ≥) = g ‚àò f :=
  rfl

theorem comp_apply (g : Œ≤ ‚Üí·µà Œ≥) (f : Œ± ‚Üí·µà Œ≤) (x : Œ±) : (g.comp f : Œ± ‚Üí Œ≥) x = g (f x) :=
  rfl

/-- Ratio of the composition `g.comp f` of two dilations is the product of their ratios. We assume
that there exist two points in `Œ±` at extended distance neither `0` nor `‚àû` because otherwise
`Dilation.ratio (g.comp f) = Dilation.ratio f = 1` while `Dilation.ratio g` can be any number. This
version works for most general spaces, see also `Dilation.ratio_comp` for a version assuming that
`Œ±` is a nontrivial metric space. -/
theorem ratio_comp' {g : Œ≤ ‚Üí·µà Œ≥} {f : Œ± ‚Üí·µà Œ≤}
    (hne : ‚àÉ x y : Œ±, edist x y ‚â† 0 ‚àß edist x y ‚â† ‚ä§) : ratio (g.comp f) = ratio g * ratio f := by
  rcases hne with ‚ü®x, y, hŒ±‚ü©
  have hgf := (edist_eq (g.comp f) x y).symm
  simp_rw [coe_comp, Function.comp, edist_eq, ‚Üê mul_assoc, ENNReal.mul_left_inj hŒ±.1 hŒ±.2]
    at hgf
  rwa [‚Üê ENNReal.coe_inj, ENNReal.coe_mul]

@[simp]
theorem comp_id (f : Œ± ‚Üí·µà Œ≤) : f.comp (Dilation.id Œ±) = f :=
  ext fun _ => rfl

@[simp]
theorem id_comp (f : Œ± ‚Üí·µà Œ≤) : (Dilation.id Œ≤).comp f = f :=
  ext fun _ => rfl

instance : Monoid (Œ± ‚Üí·µà Œ±) where
  one := Dilation.id Œ±
  mul := comp
  mul_one := comp_id
  one_mul := id_comp
  mul_assoc _ _ _ := comp_assoc _ _ _

theorem one_def : (1 : Œ± ‚Üí·µà Œ±) = Dilation.id Œ± :=
  rfl

theorem mul_def (f g : Œ± ‚Üí·µà Œ±) : f * g = f.comp g :=
  rfl

@[simp]
theorem coe_one : ‚áë(1 : Œ± ‚Üí·µà Œ±) = id :=
  rfl

@[simp]
theorem coe_mul (f g : Œ± ‚Üí·µà Œ±) : ‚áë(f * g) = f ‚àò g :=
  rfl

@[simp] theorem ratio_one : ratio (1 : Œ± ‚Üí·µà Œ±) = 1 := ratio_id

@[simp]
theorem ratio_mul (f g : Œ± ‚Üí·µà Œ±) : ratio (f * g) = ratio f * ratio g := by
  by_cases h : ‚àÄ x y : Œ±, edist x y = 0 ‚à® edist x y = ‚àû
  ¬∑ simp [ratio_of_trivial, h]
  push_neg at h
  exact ratio_comp' h

/-- `Dilation.ratio` as a monoid homomorphism from `Œ± ‚Üí·µà Œ±` to `‚Ñù‚â•0`. -/
@[simps]
def ratioHom : (Œ± ‚Üí·µà Œ±) ‚Üí* ‚Ñù‚â•0 := ‚ü®‚ü®ratio, ratio_one‚ü©, ratio_mul‚ü©

@[simp]
theorem ratio_pow (f : Œ± ‚Üí·µà Œ±) (n : ‚Ñï) : ratio (f ^ n) = ratio f ^ n :=
  ratioHom.map_pow _ _

@[simp]
theorem cancel_right {g‚ÇÅ g‚ÇÇ : Œ≤ ‚Üí·µà Œ≥} {f : Œ± ‚Üí·µà Œ≤} (hf : Surjective f) :
    g‚ÇÅ.comp f = g‚ÇÇ.comp f ‚Üî g‚ÇÅ = g‚ÇÇ :=
  ‚ü®fun h => Dilation.ext <| hf.forall.2 (Dilation.ext_iff.1 h), fun h => h ‚ñ∏ rfl‚ü©

@[simp]
theorem cancel_left {g : Œ≤ ‚Üí·µà Œ≥} {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí·µà Œ≤} (hg : Injective g) :
    g.comp f‚ÇÅ = g.comp f‚ÇÇ ‚Üî f‚ÇÅ = f‚ÇÇ :=
  ‚ü®fun h => Dilation.ext fun x => hg <| by rw [‚Üê comp_apply, h, comp_apply], fun h => h ‚ñ∏ rfl‚ü©

/-- A dilation from a metric space is a uniform inducing map -/
theorem isUniformInducing : IsUniformInducing (f : Œ± ‚Üí Œ≤) :=
  (antilipschitz f).isUniformInducing (lipschitz f).uniformContinuous

theorem tendsto_nhds_iff {Œπ : Type*} {g : Œπ ‚Üí Œ±} {a : Filter Œπ} {b : Œ±} :
    Filter.Tendsto g a (ùìù b) ‚Üî Filter.Tendsto ((f : Œ± ‚Üí Œ≤) ‚àò g) a (ùìù (f b)) :=
  (Dilation.isUniformInducing f).isInducing.tendsto_nhds_iff

/-- A dilation is continuous. -/
theorem toContinuous : Continuous (f : Œ± ‚Üí Œ≤) :=
  (lipschitz f).continuous

/-- Dilations scale the diameter by `ratio f` in pseudoemetric spaces. -/
theorem ediam_image (s : Set Œ±) : EMetric.diam ((f : Œ± ‚Üí Œ≤) '' s) = ratio f * EMetric.diam s := by
  refine ((lipschitz f).ediam_image_le s).antisymm ?_
  apply ENNReal.mul_le_of_le_div'
  rw [div_eq_mul_inv, mul_comm, ‚Üê ENNReal.coe_inv]
  exacts [(antilipschitz f).le_mul_ediam_image s, ratio_ne_zero f]

/-- A dilation scales the diameter of the range by `ratio f`. -/
theorem ediam_range : EMetric.diam (range (f : Œ± ‚Üí Œ≤)) = ratio f * EMetric.diam (univ : Set Œ±) := by
  rw [‚Üê image_univ]; exact ediam_image f univ

/-- A dilation maps balls to balls and scales the radius by `ratio f`. -/
theorem mapsTo_emetric_ball (x : Œ±) (r : ‚Ñù‚â•0‚àû) :
    MapsTo (f : Œ± ‚Üí Œ≤) (EMetric.ball x r) (EMetric.ball (f x) (ratio f * r)) :=
  fun y hy => (edist_eq f y x).trans_lt <|
    (ENNReal.mul_lt_mul_left (ENNReal.coe_ne_zero.2 <| ratio_ne_zero f) ENNReal.coe_ne_top).2 hy

/-- A dilation maps closed balls to closed balls and scales the radius by `ratio f`. -/
theorem mapsTo_emetric_closedBall (x : Œ±) (r' : ‚Ñù‚â•0‚àû) :
    MapsTo (f : Œ± ‚Üí Œ≤) (EMetric.closedBall x r') (EMetric.closedBall (f x) (ratio f * r')) :=
  fun y hy => (edist_eq f y x).trans_le <| mul_le_mul_left' hy _

theorem comp_continuousOn_iff {Œ≥} [TopologicalSpace Œ≥] {g : Œ≥ ‚Üí Œ±} {s : Set Œ≥} :
    ContinuousOn ((f : Œ± ‚Üí Œ≤) ‚àò g) s ‚Üî ContinuousOn g s :=
  (Dilation.isUniformInducing f).isInducing.continuousOn_iff.symm

theorem comp_continuous_iff {Œ≥} [TopologicalSpace Œ≥] {g : Œ≥ ‚Üí Œ±} :
    Continuous ((f : Œ± ‚Üí Œ≤) ‚àò g) ‚Üî Continuous g :=
  (Dilation.isUniformInducing f).isInducing.continuous_iff.symm

end PseudoEmetricDilation

section EmetricDilation

variable [EMetricSpace Œ±]
variable [FunLike F Œ± Œ≤]

/-- A dilation from a metric space is a uniform embedding -/
lemma isUniformEmbedding [PseudoEMetricSpace Œ≤] [DilationClass F Œ± Œ≤] (f : F) :
    IsUniformEmbedding f :=
  (antilipschitz f).isUniformEmbedding (lipschitz f).uniformContinuous

/-- A dilation from a metric space is an embedding -/
theorem isEmbedding [PseudoEMetricSpace Œ≤] [DilationClass F Œ± Œ≤] (f : F) :
    IsEmbedding (f : Œ± ‚Üí Œ≤) :=
  (Dilation.isUniformEmbedding f).isEmbedding

@[deprecated (since := "2024-10-26")]
alias embedding := isEmbedding

/-- A dilation from a complete emetric space is a closed embedding -/
lemma isClosedEmbedding [CompleteSpace Œ±] [EMetricSpace Œ≤] [DilationClass F Œ± Œ≤] (f : F) :
    IsClosedEmbedding f :=
  (antilipschitz f).isClosedEmbedding (lipschitz f).uniformContinuous

end EmetricDilation

/-- Ratio of the composition `g.comp f` of two dilations is the product of their ratios. We assume
that the domain `Œ±` of `f` is a nontrivial metric space, otherwise
`Dilation.ratio f = Dilation.ratio (g.comp f) = 1` but `Dilation.ratio g` may have any value.

See also `Dilation.ratio_comp'` for a version that works for more general spaces. -/
@[simp]
theorem ratio_comp [MetricSpace Œ±] [Nontrivial Œ±] [PseudoEMetricSpace Œ≤]
    [PseudoEMetricSpace Œ≥] {g : Œ≤ ‚Üí·µà Œ≥} {f : Œ± ‚Üí·µà Œ≤} : ratio (g.comp f) = ratio g * ratio f :=
  ratio_comp' <|
    let ‚ü®x, y, hne‚ü© := exists_pair_ne Œ±; ‚ü®x, y, mt edist_eq_zero.1 hne, edist_ne_top _ _‚ü©

section PseudoMetricDilation

variable [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] [FunLike F Œ± Œ≤] [DilationClass F Œ± Œ≤] (f : F)

/-- A dilation scales the diameter by `ratio f` in pseudometric spaces. -/
theorem diam_image (s : Set Œ±) : Metric.diam ((f : Œ± ‚Üí Œ≤) '' s) = ratio f * Metric.diam s := by
  simp [Metric.diam, ediam_image, ENNReal.toReal_mul]

theorem diam_range : Metric.diam (range (f : Œ± ‚Üí Œ≤)) = ratio f * Metric.diam (univ : Set Œ±) := by
  rw [‚Üê image_univ, diam_image]

/-- A dilation maps balls to balls and scales the radius by `ratio f`. -/
theorem mapsTo_ball (x : Œ±) (r' : ‚Ñù) :
    MapsTo (f : Œ± ‚Üí Œ≤) (Metric.ball x r') (Metric.ball (f x) (ratio f * r')) :=
  fun y hy => (dist_eq f y x).trans_lt <| (mul_lt_mul_left <| NNReal.coe_pos.2 <| ratio_pos f).2 hy

/-- A dilation maps spheres to spheres and scales the radius by `ratio f`. -/
theorem mapsTo_sphere (x : Œ±) (r' : ‚Ñù) :
    MapsTo (f : Œ± ‚Üí Œ≤) (Metric.sphere x r') (Metric.sphere (f x) (ratio f * r')) :=
  fun y hy => Metric.mem_sphere.mp hy ‚ñ∏ dist_eq f y x

/-- A dilation maps closed balls to closed balls and scales the radius by `ratio f`. -/
theorem mapsTo_closedBall (x : Œ±) (r' : ‚Ñù) :
    MapsTo (f : Œ± ‚Üí Œ≤) (Metric.closedBall x r') (Metric.closedBall (f x) (ratio f * r')) :=
  fun y hy => (dist_eq f y x).trans_le <| mul_le_mul_of_nonneg_left hy (NNReal.coe_nonneg _)

lemma tendsto_cobounded : Filter.Tendsto f (cobounded Œ±) (cobounded Œ≤) :=
  (Dilation.antilipschitz f).tendsto_cobounded

@[simp]
lemma comap_cobounded : Filter.comap f (cobounded Œ≤) = cobounded Œ± :=
  le_antisymm (lipschitz f).comap_cobounded_le (tendsto_cobounded f).le_comap

end PseudoMetricDilation

end Dilation
