/-
Copyright (c) 2018 Rohan Mitta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Rohan Mitta, Kevin Buzzard, Alistair Tucker, Johannes H√∂lzl, Yury Kudryashov
-/
import Mathlib.Data.Set.Intervals.ProjIcc
import Mathlib.Topology.Algebra.Order.Field
import Mathlib.Topology.Bornology.Hom
import Mathlib.Topology.MetricSpace.Basic
import Mathlib.Topology.MetricSpace.Bounded

#align_import topology.metric_space.lipschitz from "leanprover-community/mathlib"@"c8f305514e0d47dfaa710f5a52f0d21b588e6328"

/-!
# Lipschitz continuous functions

A map `f : Œ± ‚Üí Œ≤` between two (extended) metric spaces is called *Lipschitz continuous*
with constant `K ‚â• 0` if for all `x, y` we have `edist (f x) (f y) ‚â§ K * edist x y`.
For a metric space, the latter inequality is equivalent to `dist (f x) (f y) ‚â§ K * dist x y`.
There is also a version asserting this inequality only for `x` and `y` in some set `s`.
Finally, `f : Œ± ‚Üí Œ≤` is called *locally Lipschitz continuous* if each `x : Œ±` has a neighbourhood
on which `f` is Lipschitz continuous (with some constant).

In this file we provide various ways to prove that various combinations of Lipschitz continuous
functions are Lipschitz continuous. We also prove that Lipschitz continuous functions are
uniformly continuous, and that locally Lipschitz functions are continuous.

## Main definitions and lemmas

* `LipschitzWith K f`: states that `f` is Lipschitz with constant `K : ‚Ñù‚â•0`
* `LipschitzOnWith K f s`: states that `f` is Lipschitz with constant `K : ‚Ñù‚â•0` on a set `s`
* `LipschitzWith.uniformContinuous`: a Lipschitz function is uniformly continuous
* `LipschitzOnWith.uniformContinuousOn`: a function which is Lipschitz on a set `s` is uniformly
  continuous on `s`.
* `LocallyLipschitz f`: states that `f` is locally Lipschitz
* `LocallyLipschitz.continuous`: a locally Lipschitz function is continuous.


## Implementation notes

The parameter `K` has type `‚Ñù‚â•0`. This way we avoid conjunction in the definition and have
coercions both to `‚Ñù` and `‚Ñù‚â•0‚àû`. Constructors whose names end with `'` take `K : ‚Ñù` as an
argument, and return `LipschitzWith (Real.toNNReal K) f`.
-/

set_option autoImplicit true

universe u v w x

open Filter Function Set Topology NNReal ENNReal Bornology

variable {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w} {Œπ : Type x}

/-- A function `f` is **Lipschitz continuous** with constant `K ‚â• 0` if for all `x, y`
we have `dist (f x) (f y) ‚â§ K * dist x y`. -/
def LipschitzWith [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] (K : ‚Ñù‚â•0) (f : Œ± ‚Üí Œ≤) :=
  ‚àÄ x y, edist (f x) (f y) ‚â§ K * edist x y
#align lipschitz_with LipschitzWith

theorem lipschitzWith_iff_dist_le_mul [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] {K : ‚Ñù‚â•0}
    {f : Œ± ‚Üí Œ≤} : LipschitzWith K f ‚Üî ‚àÄ x y, dist (f x) (f y) ‚â§ K * dist x y := by
  simp only [LipschitzWith, edist_nndist, dist_nndist]
  norm_cast
#align lipschitz_with_iff_dist_le_mul lipschitzWith_iff_dist_le_mul

alias ‚ü®LipschitzWith.dist_le_mul, LipschitzWith.of_dist_le_mul‚ü© := lipschitzWith_iff_dist_le_mul
#align lipschitz_with.dist_le_mul LipschitzWith.dist_le_mul
#align lipschitz_with.of_dist_le_mul LipschitzWith.of_dist_le_mul

/-- A function `f` is **Lipschitz continuous** with constant `K ‚â• 0` **on `s`** if
for all `x, y` in `s` we have `dist (f x) (f y) ‚â§ K * dist x y`. -/
def LipschitzOnWith [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] (K : ‚Ñù‚â•0) (f : Œ± ‚Üí Œ≤)
    (s : Set Œ±) :=
  ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí ‚àÄ ‚¶Éy‚¶Ñ, y ‚àà s ‚Üí edist (f x) (f y) ‚â§ K * edist x y
#align lipschitz_on_with LipschitzOnWith

/-- `f : Œ± ‚Üí Œ≤` is called **locally Lipschitz continuous** iff every point `x`
has a neighourhood on which `f` is Lipschitz. -/
def LocallyLipschitz [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] (f : Œ± ‚Üí Œ≤) : Prop :=
  ‚àÄ x : Œ±, ‚àÉ K, ‚àÉ t ‚àà ùìù x, LipschitzOnWith K f t

/-- Every function is Lipschitz on the empty set (with any Lipschitz constant). -/
@[simp]
theorem lipschitzOnWith_empty [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] (K : ‚Ñù‚â•0) (f : Œ± ‚Üí Œ≤) :
    LipschitzOnWith K f ‚àÖ := fun _ => False.elim
#align lipschitz_on_with_empty lipschitzOnWith_empty

/-- Being Lipschitz on a set is monotone w.r.t. that set. -/
theorem LipschitzOnWith.mono [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] {K : ‚Ñù‚â•0} {s t : Set Œ±}
    {f : Œ± ‚Üí Œ≤} (hf : LipschitzOnWith K f t) (h : s ‚äÜ t) : LipschitzOnWith K f s :=
  fun _x x_in _y y_in => hf (h x_in) (h y_in)
#align lipschitz_on_with.mono LipschitzOnWith.mono

theorem lipschitzOnWith_iff_dist_le_mul [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] {K : ‚Ñù‚â•0}
    {s : Set Œ±} {f : Œ± ‚Üí Œ≤} :
    LipschitzOnWith K f s ‚Üî ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, dist (f x) (f y) ‚â§ K * dist x y := by
  simp only [LipschitzOnWith, edist_nndist, dist_nndist]
  norm_cast
#align lipschitz_on_with_iff_dist_le_mul lipschitzOnWith_iff_dist_le_mul

alias ‚ü®LipschitzOnWith.dist_le_mul, LipschitzOnWith.of_dist_le_mul‚ü© :=
  lipschitzOnWith_iff_dist_le_mul
#align lipschitz_on_with.dist_le_mul LipschitzOnWith.dist_le_mul
#align lipschitz_on_with.of_dist_le_mul LipschitzOnWith.of_dist_le_mul

/-- `f` is Lipschitz iff it is Lipschitz on the entire space. -/
@[simp]
theorem lipschitzOn_univ [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] {K : ‚Ñù‚â•0} {f : Œ± ‚Üí Œ≤} :
    LipschitzOnWith K f univ ‚Üî LipschitzWith K f := by simp [LipschitzOnWith, LipschitzWith]
#align lipschitz_on_univ lipschitzOn_univ

theorem lipschitzOnWith_iff_restrict [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] {K : ‚Ñù‚â•0}
    {f : Œ± ‚Üí Œ≤} {s : Set Œ±} : LipschitzOnWith K f s ‚Üî LipschitzWith K (s.restrict f) := by
  simp only [LipschitzOnWith, LipschitzWith, SetCoe.forall', restrict, Subtype.edist_eq]
#align lipschitz_on_with_iff_restrict lipschitzOnWith_iff_restrict

alias ‚ü®LipschitzOnWith.to_restrict, _‚ü© := lipschitzOnWith_iff_restrict
#align lipschitz_on_with.to_restrict LipschitzOnWith.to_restrict

theorem MapsTo.lipschitzOnWith_iff_restrict [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] {K : ‚Ñù‚â•0}
    {f : Œ± ‚Üí Œ≤} {s : Set Œ±} {t : Set Œ≤} (h : MapsTo f s t) :
    LipschitzOnWith K f s ‚Üî LipschitzWith K (h.restrict f s t) :=
  _root_.lipschitzOnWith_iff_restrict
#align maps_to.lipschitz_on_with_iff_restrict MapsTo.lipschitzOnWith_iff_restrict

alias ‚ü®LipschitzOnWith.to_restrict_mapsTo, _‚ü© := MapsTo.lipschitzOnWith_iff_restrict
#align lipschitz_on_with.to_restrict_maps_to LipschitzOnWith.to_restrict_mapsTo

namespace LipschitzWith

section EMetric

open EMetric

variable [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] [PseudoEMetricSpace Œ≥]

variable {K : ‚Ñù‚â•0} {f : Œ± ‚Üí Œ≤} {x y : Œ±} {r : ‚Ñù‚â•0‚àû}

protected theorem lipschitzOnWith (h : LipschitzWith K f) (s : Set Œ±) : LipschitzOnWith K f s :=
  fun x _ y _ => h x y
#align lipschitz_with.lipschitz_on_with LipschitzWith.lipschitzOnWith

theorem edist_le_mul (h : LipschitzWith K f) (x y : Œ±) : edist (f x) (f y) ‚â§ K * edist x y :=
  h x y
#align lipschitz_with.edist_le_mul LipschitzWith.edist_le_mul

theorem edist_le_mul_of_le (h : LipschitzWith K f) (hr : edist x y ‚â§ r) :
    edist (f x) (f y) ‚â§ K * r :=
  (h x y).trans <| ENNReal.mul_left_mono hr
#align lipschitz_with.edist_le_mul_of_le LipschitzWith.edist_le_mul_of_le

theorem edist_lt_mul_of_lt (h : LipschitzWith K f) (hK : K ‚â† 0) (hr : edist x y < r) :
    edist (f x) (f y) < K * r :=
  (h x y).trans_lt <| (ENNReal.mul_lt_mul_left (ENNReal.coe_ne_zero.2 hK) ENNReal.coe_ne_top).2 hr
#align lipschitz_with.edist_lt_mul_of_lt LipschitzWith.edist_lt_mul_of_lt

theorem mapsTo_emetric_closedBall (h : LipschitzWith K f) (x : Œ±) (r : ‚Ñù‚â•0‚àû) :
    MapsTo f (closedBall x r) (closedBall (f x) (K * r)) := fun _y hy => h.edist_le_mul_of_le hy
#align lipschitz_with.maps_to_emetric_closed_ball LipschitzWith.mapsTo_emetric_closedBall

theorem mapsTo_emetric_ball (h : LipschitzWith K f) (hK : K ‚â† 0) (x : Œ±) (r : ‚Ñù‚â•0‚àû) :
    MapsTo f (ball x r) (ball (f x) (K * r)) := fun _y hy => h.edist_lt_mul_of_lt hK hy
#align lipschitz_with.maps_to_emetric_ball LipschitzWith.mapsTo_emetric_ball

theorem edist_lt_top (hf : LipschitzWith K f) {x y : Œ±} (h : edist x y ‚â† ‚ä§) :
    edist (f x) (f y) < ‚ä§ :=
  (hf x y).trans_lt <| ENNReal.mul_lt_top ENNReal.coe_ne_top h
#align lipschitz_with.edist_lt_top LipschitzWith.edist_lt_top

theorem mul_edist_le (h : LipschitzWith K f) (x y : Œ±) :
    (K‚Åª¬π : ‚Ñù‚â•0‚àû) * edist (f x) (f y) ‚â§ edist x y := by
  rw [mul_comm, ‚Üê div_eq_mul_inv]
  exact ENNReal.div_le_of_le_mul' (h x y)
#align lipschitz_with.mul_edist_le LipschitzWith.mul_edist_le

protected theorem of_edist_le (h : ‚àÄ x y, edist (f x) (f y) ‚â§ edist x y) : LipschitzWith 1 f :=
  fun x y => by simp only [ENNReal.coe_one, one_mul, h]
#align lipschitz_with.of_edist_le LipschitzWith.of_edist_le

protected theorem weaken (hf : LipschitzWith K f) {K' : ‚Ñù‚â•0} (h : K ‚â§ K') : LipschitzWith K' f :=
  fun x y => le_trans (hf x y) <| ENNReal.mul_right_mono (ENNReal.coe_le_coe.2 h)
#align lipschitz_with.weaken LipschitzWith.weaken

theorem ediam_image_le (hf : LipschitzWith K f) (s : Set Œ±) :
    EMetric.diam (f '' s) ‚â§ K * EMetric.diam s := by
  apply EMetric.diam_le
  rintro _ ‚ü®x, hx, rfl‚ü© _ ‚ü®y, hy, rfl‚ü©
  exact hf.edist_le_mul_of_le (EMetric.edist_le_diam_of_mem hx hy)
#align lipschitz_with.ediam_image_le LipschitzWith.ediam_image_le

theorem edist_lt_of_edist_lt_div (hf : LipschitzWith K f) {x y : Œ±} {d : ‚Ñù‚â•0‚àû}
    (h : edist x y < d / K) : edist (f x) (f y) < d :=
  calc
    edist (f x) (f y) ‚â§ K * edist x y := hf x y
    _ < d := ENNReal.mul_lt_of_lt_div' h
#align lipschitz_with.edist_lt_of_edist_lt_div LipschitzWith.edist_lt_of_edist_lt_div

/-- A Lipschitz function is uniformly continuous. -/
protected theorem uniformContinuous (hf : LipschitzWith K f) : UniformContinuous f :=
  EMetric.uniformContinuous_iff.2 fun Œµ Œµpos =>
    ‚ü®Œµ / K, ENNReal.div_pos_iff.2 ‚ü®ne_of_gt Œµpos, ENNReal.coe_ne_top‚ü©, hf.edist_lt_of_edist_lt_div‚ü©
#align lipschitz_with.uniform_continuous LipschitzWith.uniformContinuous

/-- A Lipschitz function is continuous. -/
protected theorem continuous (hf : LipschitzWith K f) : Continuous f :=
  hf.uniformContinuous.continuous
#align lipschitz_with.continuous LipschitzWith.continuous

/-- Constant functions are Lipschitz (with any constant). -/
protected theorem const (b : Œ≤) : LipschitzWith 0 fun _ : Œ± => b := fun x y => by
  simp only [edist_self, zero_le]
#align lipschitz_with.const LipschitzWith.const

protected theorem const' (b : Œ≤) {K : ‚Ñù‚â•0} : LipschitzWith K fun _ : Œ± => b := fun x y => by
  simp only [edist_self, zero_le]

/-- The identity is 1-Lipschitz. -/
protected theorem id : LipschitzWith 1 (@id Œ±) :=
  LipschitzWith.of_edist_le fun _ _ => le_rfl
#align lipschitz_with.id LipschitzWith.id

/-- The inclusion of a subset is 1-Lipschitz. -/
protected theorem subtype_val (s : Set Œ±) : LipschitzWith 1 (Subtype.val : s ‚Üí Œ±) :=
  LipschitzWith.of_edist_le fun _ _ => le_rfl
#align lipschitz_with.subtype_val LipschitzWith.subtype_val
#align lipschitz_with.subtype_coe LipschitzWith.subtype_val

theorem subtype_mk (hf : LipschitzWith K f) {p : Œ≤ ‚Üí Prop} (hp : ‚àÄ x, p (f x)) :
    LipschitzWith K (fun x => ‚ü®f x, hp x‚ü© : Œ± ‚Üí { y // p y }) :=
  hf
#align lipschitz_with.subtype_mk LipschitzWith.subtype_mk

protected theorem eval {Œ± : Œπ ‚Üí Type u} [‚àÄ i, PseudoEMetricSpace (Œ± i)] [Fintype Œπ] (i : Œπ) :
    LipschitzWith 1 (Function.eval i : (‚àÄ i, Œ± i) ‚Üí Œ± i) :=
  LipschitzWith.of_edist_le fun f g => by convert edist_le_pi_edist f g i
#align lipschitz_with.eval LipschitzWith.eval

/-- The restriction of a `K`-Lipschitz function is `K`-Lipschitz. -/
protected theorem restrict (hf : LipschitzWith K f) (s : Set Œ±) : LipschitzWith K (s.restrict f) :=
  fun x y => hf x y
#align lipschitz_with.restrict LipschitzWith.restrict

/-- The composition of Lipschitz functions is Lipschitz. -/
protected theorem comp {Kf Kg : ‚Ñù‚â•0} {f : Œ≤ ‚Üí Œ≥} {g : Œ± ‚Üí Œ≤} (hf : LipschitzWith Kf f)
    (hg : LipschitzWith Kg g) : LipschitzWith (Kf * Kg) (f ‚àò g) := fun x y =>
  calc
    edist (f (g x)) (f (g y)) ‚â§ Kf * edist (g x) (g y) := hf _ _
    _ ‚â§ Kf * (Kg * edist x y) := (ENNReal.mul_left_mono (hg _ _))
    _ = (Kf * Kg : ‚Ñù‚â•0) * edist x y := by rw [‚Üê mul_assoc, ENNReal.coe_mul]
#align lipschitz_with.comp LipschitzWith.comp

theorem comp_lipschitzOnWith {Kf Kg : ‚Ñù‚â•0} {f : Œ≤ ‚Üí Œ≥} {g : Œ± ‚Üí Œ≤} {s : Set Œ±}
    (hf : LipschitzWith Kf f) (hg : LipschitzOnWith Kg g s) : LipschitzOnWith (Kf * Kg) (f ‚àò g) s :=
  lipschitzOnWith_iff_restrict.mpr <| hf.comp hg.to_restrict
#align lipschitz_with.comp_lipschitz_on_with LipschitzWith.comp_lipschitzOnWith

protected theorem prod_fst : LipschitzWith 1 (@Prod.fst Œ± Œ≤) :=
  LipschitzWith.of_edist_le fun _ _ => le_max_left _ _
#align lipschitz_with.prod_fst LipschitzWith.prod_fst

protected theorem prod_snd : LipschitzWith 1 (@Prod.snd Œ± Œ≤) :=
  LipschitzWith.of_edist_le fun _ _ => le_max_right _ _
#align lipschitz_with.prod_snd LipschitzWith.prod_snd

/-- If `f` and `g` are Lipschitz functions, so is the induced map `f √ó g` to the product type. -/
protected theorem prod {f : Œ± ‚Üí Œ≤} {Kf : ‚Ñù‚â•0} (hf : LipschitzWith Kf f) {g : Œ± ‚Üí Œ≥} {Kg : ‚Ñù‚â•0}
    (hg : LipschitzWith Kg g) : LipschitzWith (max Kf Kg) fun x => (f x, g x) := by
  intro x y
  rw [ENNReal.coe_mono.map_max, Prod.edist_eq, ENNReal.max_mul]
  exact max_le_max (hf x y) (hg x y)
#align lipschitz_with.prod LipschitzWith.prod

protected theorem prod_mk_left (a : Œ±) : LipschitzWith 1 (Prod.mk a : Œ≤ ‚Üí Œ± √ó Œ≤) := by
  simpa only [max_eq_right zero_le_one] using (LipschitzWith.const a).prod LipschitzWith.id
#align lipschitz_with.prod_mk_left LipschitzWith.prod_mk_left

protected theorem prod_mk_right (b : Œ≤) : LipschitzWith 1 fun a : Œ± => (a, b) := by
  simpa only [max_eq_left zero_le_one] using LipschitzWith.id.prod (LipschitzWith.const b)
#align lipschitz_with.prod_mk_right LipschitzWith.prod_mk_right

protected theorem uncurry {f : Œ± ‚Üí Œ≤ ‚Üí Œ≥} {KŒ± KŒ≤ : ‚Ñù‚â•0} (hŒ± : ‚àÄ b, LipschitzWith KŒ± fun a => f a b)
    (hŒ≤ : ‚àÄ a, LipschitzWith KŒ≤ (f a)) : LipschitzWith (KŒ± + KŒ≤) (Function.uncurry f) := by
  rintro ‚ü®a‚ÇÅ, b‚ÇÅ‚ü© ‚ü®a‚ÇÇ, b‚ÇÇ‚ü©
  simp only [Function.uncurry, ENNReal.coe_add, add_mul]
  apply le_trans (edist_triangle _ (f a‚ÇÇ b‚ÇÅ) _)
  exact
    add_le_add (le_trans (hŒ± _ _ _) <| ENNReal.mul_left_mono <| le_max_left _ _)
      (le_trans (hŒ≤ _ _ _) <| ENNReal.mul_left_mono <| le_max_right _ _)
#align lipschitz_with.uncurry LipschitzWith.uncurry

/-- Iterates of a Lipschitz function are Lipschitz. -/
protected theorem iterate {f : Œ± ‚Üí Œ±} (hf : LipschitzWith K f) : ‚àÄ n, LipschitzWith (K ^ n) f^[n]
  | 0 => by simpa only [pow_zero] using LipschitzWith.id
  | n + 1 => by rw [pow_succ']; exact (LipschitzWith.iterate hf n).comp hf
#align lipschitz_with.iterate LipschitzWith.iterate

theorem edist_iterate_succ_le_geometric {f : Œ± ‚Üí Œ±} (hf : LipschitzWith K f) (x n) :
    edist (f^[n] x) (f^[n + 1] x) ‚â§ edist x (f x) * (K : ‚Ñù‚â•0‚àû) ^ n := by
  rw [iterate_succ, mul_comm]
  simpa only [ENNReal.coe_pow] using (hf.iterate n) x (f x)
#align lipschitz_with.edist_iterate_succ_le_geometric LipschitzWith.edist_iterate_succ_le_geometric

protected theorem mul_end {f g : Function.End Œ±} {Kf Kg} (hf : LipschitzWith Kf f)
    (hg : LipschitzWith Kg g) : LipschitzWith (Kf * Kg) (f * g : Function.End Œ±) :=
  hf.comp hg
#align lipschitz_with.mul LipschitzWith.mul_end

/-- The product of a list of Lipschitz continuous endomorphisms is a Lipschitz continuous
endomorphism. -/
protected theorem list_prod (f : Œπ ‚Üí Function.End Œ±) (K : Œπ ‚Üí ‚Ñù‚â•0)
    (h : ‚àÄ i, LipschitzWith (K i) (f i)) : ‚àÄ l : List Œπ, LipschitzWith (l.map K).prod (l.map f).prod
  | [] => by simpa using LipschitzWith.id
  | i::l => by
    simp only [List.map_cons, List.prod_cons]
    exact (h i).mul_end (LipschitzWith.list_prod f K h l)
#align lipschitz_with.list_prod LipschitzWith.list_prod

protected theorem pow_end {f : Function.End Œ±} {K} (h : LipschitzWith K f) :
    ‚àÄ n : ‚Ñï, LipschitzWith (K ^ n) (f ^ n : Function.End Œ±)
  | 0 => by simpa only [pow_zero] using LipschitzWith.id
  | n + 1 => by
    rw [pow_succ, pow_succ]
    exact h.mul_end (LipschitzWith.pow_end h n)
#align lipschitz_with.pow LipschitzWith.pow_end

end EMetric

section Metric

variable [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] [PseudoMetricSpace Œ≥] {K : ‚Ñù‚â•0} {f : Œ± ‚Üí Œ≤}
  {x y : Œ±} {r : ‚Ñù}

protected theorem of_dist_le' {K : ‚Ñù} (h : ‚àÄ x y, dist (f x) (f y) ‚â§ K * dist x y) :
    LipschitzWith (Real.toNNReal K) f :=
  of_dist_le_mul fun x y =>
    le_trans (h x y) <| by gcongr; apply Real.le_coe_toNNReal
#align lipschitz_with.of_dist_le' LipschitzWith.of_dist_le'

protected theorem mk_one (h : ‚àÄ x y, dist (f x) (f y) ‚â§ dist x y) : LipschitzWith 1 f :=
  of_dist_le_mul <| by simpa only [NNReal.coe_one, one_mul] using h
#align lipschitz_with.mk_one LipschitzWith.mk_one

/-- For functions to `‚Ñù`, it suffices to prove `f x ‚â§ f y + K * dist x y`; this version
doesn't assume `0‚â§K`. -/
protected theorem of_le_add_mul' {f : Œ± ‚Üí ‚Ñù} (K : ‚Ñù) (h : ‚àÄ x y, f x ‚â§ f y + K * dist x y) :
    LipschitzWith (Real.toNNReal K) f :=
  have I : ‚àÄ x y, f x - f y ‚â§ K * dist x y := fun x y => sub_le_iff_le_add'.2 (h x y)
  LipschitzWith.of_dist_le' fun x y => abs_sub_le_iff.2 ‚ü®I x y, dist_comm y x ‚ñ∏ I y x‚ü©
#align lipschitz_with.of_le_add_mul' LipschitzWith.of_le_add_mul'

/-- For functions to `‚Ñù`, it suffices to prove `f x ‚â§ f y + K * dist x y`; this version
assumes `0‚â§K`. -/
protected theorem of_le_add_mul {f : Œ± ‚Üí ‚Ñù} (K : ‚Ñù‚â•0) (h : ‚àÄ x y, f x ‚â§ f y + K * dist x y) :
    LipschitzWith K f := by simpa only [Real.toNNReal_coe] using LipschitzWith.of_le_add_mul' K h
#align lipschitz_with.of_le_add_mul LipschitzWith.of_le_add_mul

protected theorem of_le_add {f : Œ± ‚Üí ‚Ñù} (h : ‚àÄ x y, f x ‚â§ f y + dist x y) : LipschitzWith 1 f :=
  LipschitzWith.of_le_add_mul 1 <| by simpa only [NNReal.coe_one, one_mul]
#align lipschitz_with.of_le_add LipschitzWith.of_le_add

protected theorem le_add_mul {f : Œ± ‚Üí ‚Ñù} {K : ‚Ñù‚â•0} (h : LipschitzWith K f) (x y) :
    f x ‚â§ f y + K * dist x y :=
  sub_le_iff_le_add'.1 <| le_trans (le_abs_self _) <| h.dist_le_mul x y
#align lipschitz_with.le_add_mul LipschitzWith.le_add_mul

protected theorem iff_le_add_mul {f : Œ± ‚Üí ‚Ñù} {K : ‚Ñù‚â•0} :
    LipschitzWith K f ‚Üî ‚àÄ x y, f x ‚â§ f y + K * dist x y :=
  ‚ü®LipschitzWith.le_add_mul, LipschitzWith.of_le_add_mul K‚ü©
#align lipschitz_with.iff_le_add_mul LipschitzWith.iff_le_add_mul

theorem nndist_le (hf : LipschitzWith K f) (x y : Œ±) : nndist (f x) (f y) ‚â§ K * nndist x y :=
  hf.dist_le_mul x y
#align lipschitz_with.nndist_le LipschitzWith.nndist_le

theorem dist_le_mul_of_le (hf : LipschitzWith K f) (hr : dist x y ‚â§ r) : dist (f x) (f y) ‚â§ K * r :=
  (hf.dist_le_mul x y).trans <| by gcongr
#align lipschitz_with.dist_le_mul_of_le LipschitzWith.dist_le_mul_of_le

theorem mapsTo_closedBall (hf : LipschitzWith K f) (x : Œ±) (r : ‚Ñù) :
    MapsTo f (Metric.closedBall x r) (Metric.closedBall (f x) (K * r)) := fun _y hy =>
  hf.dist_le_mul_of_le hy
#align lipschitz_with.maps_to_closed_ball LipschitzWith.mapsTo_closedBall

theorem dist_lt_mul_of_lt (hf : LipschitzWith K f) (hK : K ‚â† 0) (hr : dist x y < r) :
    dist (f x) (f y) < K * r :=
  (hf.dist_le_mul x y).trans_lt <| (mul_lt_mul_left <| NNReal.coe_pos.2 hK.bot_lt).2 hr
#align lipschitz_with.dist_lt_mul_of_lt LipschitzWith.dist_lt_mul_of_lt

theorem mapsTo_ball (hf : LipschitzWith K f) (hK : K ‚â† 0) (x : Œ±) (r : ‚Ñù) :
    MapsTo f (Metric.ball x r) (Metric.ball (f x) (K * r)) := fun _y hy =>
  hf.dist_lt_mul_of_lt hK hy
#align lipschitz_with.maps_to_ball LipschitzWith.mapsTo_ball

/-- A Lipschitz continuous map is a locally bounded map. -/
def toLocallyBoundedMap (f : Œ± ‚Üí Œ≤) (hf : LipschitzWith K f) : LocallyBoundedMap Œ± Œ≤ :=
  LocallyBoundedMap.ofMapBounded f fun _s hs =>
    let ‚ü®C, hC‚ü© := Metric.isBounded_iff.1 hs
    Metric.isBounded_iff.2 ‚ü®K * C, ball_image_iff.2 fun _x hx => ball_image_iff.2 fun _y hy =>
      hf.dist_le_mul_of_le (hC hx hy)‚ü©
#align lipschitz_with.to_locally_bounded_map LipschitzWith.toLocallyBoundedMap

@[simp]
theorem coe_toLocallyBoundedMap (hf : LipschitzWith K f) : ‚áë(hf.toLocallyBoundedMap f) = f :=
  rfl
#align lipschitz_with.coe_to_locally_bounded_map LipschitzWith.coe_toLocallyBoundedMap

theorem comap_cobounded_le (hf : LipschitzWith K f) :
    comap f (Bornology.cobounded Œ≤) ‚â§ Bornology.cobounded Œ± :=
  (hf.toLocallyBoundedMap f).2
#align lipschitz_with.comap_cobounded_le LipschitzWith.comap_cobounded_le

/-- The image of a bounded set under a Lipschitz map is bounded. -/
theorem isBounded_image (hf : LipschitzWith K f) {s : Set Œ±} (hs : IsBounded s) :
    IsBounded (f '' s) :=
  hs.image (toLocallyBoundedMap f hf)
#align lipschitz_with.bounded_image LipschitzWith.isBounded_image

theorem diam_image_le (hf : LipschitzWith K f) (s : Set Œ±) (hs : IsBounded s) :
    Metric.diam (f '' s) ‚â§ K * Metric.diam s :=
  Metric.diam_le_of_forall_dist_le (mul_nonneg K.coe_nonneg Metric.diam_nonneg) <|
    ball_image_iff.2 fun _x hx =>
      ball_image_iff.2 fun _y hy => hf.dist_le_mul_of_le <| Metric.dist_le_diam_of_mem hs hx hy
#align lipschitz_with.diam_image_le LipschitzWith.diam_image_le

protected theorem dist_left (y : Œ±) : LipschitzWith 1 (dist ¬∑ y) :=
  LipschitzWith.mk_one fun _ _ => dist_dist_dist_le_left _ _ _
#align lipschitz_with.dist_left LipschitzWith.dist_left

protected theorem dist_right (x : Œ±) : LipschitzWith 1 (dist x) :=
  LipschitzWith.of_le_add fun _ _ => dist_triangle_right _ _ _
#align lipschitz_with.dist_right LipschitzWith.dist_right

protected theorem dist : LipschitzWith 2 (Function.uncurry <| @dist Œ± _) := by
  rw [‚Üê one_add_one_eq_two]
  exact LipschitzWith.uncurry LipschitzWith.dist_left LipschitzWith.dist_right
#align lipschitz_with.dist LipschitzWith.dist

theorem dist_iterate_succ_le_geometric {f : Œ± ‚Üí Œ±} (hf : LipschitzWith K f) (x n) :
    dist (f^[n] x) (f^[n + 1] x) ‚â§ dist x (f x) * (K : ‚Ñù) ^ n := by
  rw [iterate_succ, mul_comm]
  simpa only [NNReal.coe_pow] using (hf.iterate n).dist_le_mul x (f x)
#align lipschitz_with.dist_iterate_succ_le_geometric LipschitzWith.dist_iterate_succ_le_geometric

theorem _root_.lipschitzWith_max : LipschitzWith 1 fun p : ‚Ñù √ó ‚Ñù => max p.1 p.2 :=
  LipschitzWith.of_le_add fun _ _ => sub_le_iff_le_add'.1 <|
    (le_abs_self _).trans (abs_max_sub_max_le_max _ _ _ _)
#align lipschitz_with_max lipschitzWith_max

theorem _root_.lipschitzWith_min : LipschitzWith 1 fun p : ‚Ñù √ó ‚Ñù => min p.1 p.2 :=
  LipschitzWith.of_le_add fun _ _ => sub_le_iff_le_add'.1 <|
    (le_abs_self _).trans (abs_min_sub_min_le_max _ _ _ _)
#align lipschitz_with_min lipschitzWith_min

end Metric

section EMetric

variable [PseudoEMetricSpace Œ±] {f g : Œ± ‚Üí ‚Ñù} {Kf Kg : ‚Ñù‚â•0}

protected theorem max (hf : LipschitzWith Kf f) (hg : LipschitzWith Kg g) :
    LipschitzWith (max Kf Kg) fun x => max (f x) (g x) := by
  simpa only [(¬∑ ‚àò ¬∑), one_mul] using lipschitzWith_max.comp (hf.prod hg)
#align lipschitz_with.max LipschitzWith.max

protected theorem min (hf : LipschitzWith Kf f) (hg : LipschitzWith Kg g) :
    LipschitzWith (max Kf Kg) fun x => min (f x) (g x) := by
  simpa only [(¬∑ ‚àò ¬∑), one_mul] using lipschitzWith_min.comp (hf.prod hg)
#align lipschitz_with.min LipschitzWith.min

theorem max_const (hf : LipschitzWith Kf f) (a : ‚Ñù) : LipschitzWith Kf fun x => max (f x) a := by
  simpa only [max_eq_left (zero_le Kf)] using hf.max (LipschitzWith.const a)
#align lipschitz_with.max_const LipschitzWith.max_const

theorem const_max (hf : LipschitzWith Kf f) (a : ‚Ñù) : LipschitzWith Kf fun x => max a (f x) := by
  simpa only [max_comm] using hf.max_const a
#align lipschitz_with.const_max LipschitzWith.const_max

theorem min_const (hf : LipschitzWith Kf f) (a : ‚Ñù) : LipschitzWith Kf fun x => min (f x) a := by
  simpa only [max_eq_left (zero_le Kf)] using hf.min (LipschitzWith.const a)
#align lipschitz_with.min_const LipschitzWith.min_const

theorem const_min (hf : LipschitzWith Kf f) (a : ‚Ñù) : LipschitzWith Kf fun x => min a (f x) := by
  simpa only [min_comm] using hf.min_const a
#align lipschitz_with.const_min LipschitzWith.const_min

end EMetric

protected theorem projIcc {a b : ‚Ñù} (h : a ‚â§ b) : LipschitzWith 1 (projIcc a b h) :=
  ((LipschitzWith.id.const_min _).const_max _).subtype_mk _
#align lipschitz_with.proj_Icc LipschitzWith.projIcc

end LipschitzWith

namespace Metric

variable [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] {s : Set Œ±} {t : Set Œ≤}

#align metric.bounded.left_of_prod Bornology.IsBounded.fst_of_prod
#align metric.bounded.right_of_prod Bornology.IsBounded.snd_of_prod
#align metric.bounded_prod_of_nonempty Bornology.isBounded_prod_of_nonempty
#align metric.bounded_prod Bornology.isBounded_prod

end Metric

namespace LipschitzOnWith

section EMetric

variable [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] [PseudoEMetricSpace Œ≥]

variable {K : ‚Ñù‚â•0} {s : Set Œ±} {f : Œ± ‚Üí Œ≤}

protected theorem uniformContinuousOn (hf : LipschitzOnWith K f s) : UniformContinuousOn f s :=
  uniformContinuousOn_iff_restrict.mpr (lipschitzOnWith_iff_restrict.mp hf).uniformContinuous
#align lipschitz_on_with.uniform_continuous_on LipschitzOnWith.uniformContinuousOn

protected theorem continuousOn (hf : LipschitzOnWith K f s) : ContinuousOn f s :=
  hf.uniformContinuousOn.continuousOn
#align lipschitz_on_with.continuous_on LipschitzOnWith.continuousOn

theorem edist_le_mul_of_le (h : LipschitzOnWith K f s) (hx : x ‚àà s) (hy : y ‚àà s)
    (hr : edist x y ‚â§ r) :
    edist (f x) (f y) ‚â§ K * r :=
  (h hx hy).trans <| ENNReal.mul_left_mono hr

theorem edist_lt_of_edist_lt_div (hf : LipschitzOnWith K f s) {x y : Œ±} (hx : x ‚àà s) (hy : y ‚àà s)
    {d : ‚Ñù‚â•0‚àû} (hd : edist x y < d / K) : edist (f x) (f y) < d :=
   hf.to_restrict.edist_lt_of_edist_lt_div <|
    show edist (‚ü®x, hx‚ü© : s) ‚ü®y, hy‚ü© < d / K from hd
#align lipschitz_on_with.edist_lt_of_edist_lt_div LipschitzOnWith.edist_lt_of_edist_lt_div

protected theorem comp {g : Œ≤ ‚Üí Œ≥} {t : Set Œ≤} {Kg : ‚Ñù‚â•0} (hg : LipschitzOnWith Kg g t)
    (hf : LipschitzOnWith K f s) (hmaps : MapsTo f s t) : LipschitzOnWith (Kg * K) (g ‚àò f) s :=
  lipschitzOnWith_iff_restrict.mpr <| hg.to_restrict.comp (hf.to_restrict_mapsTo hmaps)
#align lipschitz_on_with.comp LipschitzOnWith.comp

/-- If `f` and `g` are Lipschitz on `s`, so is the induced map `f √ó g` to the product type. -/
protected theorem prod {g : Œ± ‚Üí Œ≥} {Kf Kg : ‚Ñù‚â•0} (hf : LipschitzOnWith Kf f s)
    (hg : LipschitzOnWith Kg g s) : LipschitzOnWith (max Kf Kg) (fun x => (f x, g x)) s := by
  intro _ hx _ hy
  rw [ENNReal.coe_mono.map_max, Prod.edist_eq, ENNReal.max_mul]
  exact max_le_max (hf hx hy) (hg hx hy)

theorem ediam_image2_le (f : Œ± ‚Üí Œ≤ ‚Üí Œ≥) {K‚ÇÅ K‚ÇÇ : ‚Ñù‚â•0} (s : Set Œ±) (t : Set Œ≤)
    (hf‚ÇÅ : ‚àÄ b ‚àà t, LipschitzOnWith K‚ÇÅ (fun a => f a b) s)
    (hf‚ÇÇ : ‚àÄ a ‚àà s, LipschitzOnWith K‚ÇÇ (f a) t) :
    EMetric.diam (Set.image2 f s t) ‚â§ ‚ÜëK‚ÇÅ * EMetric.diam s + ‚ÜëK‚ÇÇ * EMetric.diam t := by
  apply EMetric.diam_le
  rintro _ ‚ü®a‚ÇÅ, b‚ÇÅ, ha‚ÇÅ, hb‚ÇÅ, rfl‚ü© _ ‚ü®a‚ÇÇ, b‚ÇÇ, ha‚ÇÇ, hb‚ÇÇ, rfl‚ü©
  refine' (edist_triangle _ (f a‚ÇÇ b‚ÇÅ) _).trans _
  exact
    add_le_add
      ((hf‚ÇÅ b‚ÇÅ hb‚ÇÅ ha‚ÇÅ ha‚ÇÇ).trans <| ENNReal.mul_left_mono <| EMetric.edist_le_diam_of_mem ha‚ÇÅ ha‚ÇÇ)
      ((hf‚ÇÇ a‚ÇÇ ha‚ÇÇ hb‚ÇÅ hb‚ÇÇ).trans <| ENNReal.mul_left_mono <| EMetric.edist_le_diam_of_mem hb‚ÇÅ hb‚ÇÇ)
#align lipschitz_on_with.ediam_image2_le LipschitzOnWith.ediam_image2_le

end EMetric

section Metric

variable [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] [PseudoMetricSpace Œ≥]

variable {K : ‚Ñù‚â•0} {s : Set Œ±} {f : Œ± ‚Üí Œ≤}

protected theorem of_dist_le' {K : ‚Ñù} (h : ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, dist (f x) (f y) ‚â§ K * dist x y) :
    LipschitzOnWith (Real.toNNReal K) f s :=
  of_dist_le_mul fun x hx y hy =>
    le_trans (h x hx y hy) <| by gcongr; apply Real.le_coe_toNNReal
#align lipschitz_on_with.of_dist_le' LipschitzOnWith.of_dist_le'

protected theorem mk_one (h : ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, dist (f x) (f y) ‚â§ dist x y) :
    LipschitzOnWith 1 f s :=
  of_dist_le_mul <| by simpa only [NNReal.coe_one, one_mul] using h
#align lipschitz_on_with.mk_one LipschitzOnWith.mk_one

/-- For functions to `‚Ñù`, it suffices to prove `f x ‚â§ f y + K * dist x y`; this version
doesn't assume `0‚â§K`. -/
protected theorem of_le_add_mul' {f : Œ± ‚Üí ‚Ñù} (K : ‚Ñù)
    (h : ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, f x ‚â§ f y + K * dist x y) : LipschitzOnWith (Real.toNNReal K) f s :=
  have I : ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, f x - f y ‚â§ K * dist x y := fun x hx y hy =>
    sub_le_iff_le_add'.2 (h x hx y hy)
  LipschitzOnWith.of_dist_le' fun x hx y hy =>
    abs_sub_le_iff.2 ‚ü®I x hx y hy, dist_comm y x ‚ñ∏ I y hy x hx‚ü©
#align lipschitz_on_with.of_le_add_mul' LipschitzOnWith.of_le_add_mul'

/-- For functions to `‚Ñù`, it suffices to prove `f x ‚â§ f y + K * dist x y`; this version
assumes `0‚â§K`. -/
protected theorem of_le_add_mul {f : Œ± ‚Üí ‚Ñù} (K : ‚Ñù‚â•0)
    (h : ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, f x ‚â§ f y + K * dist x y) : LipschitzOnWith K f s := by
  simpa only [Real.toNNReal_coe] using LipschitzOnWith.of_le_add_mul' K h
#align lipschitz_on_with.of_le_add_mul LipschitzOnWith.of_le_add_mul

protected theorem of_le_add {f : Œ± ‚Üí ‚Ñù} (h : ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, f x ‚â§ f y + dist x y) :
    LipschitzOnWith 1 f s :=
  LipschitzOnWith.of_le_add_mul 1 <| by simpa only [NNReal.coe_one, one_mul]
#align lipschitz_on_with.of_le_add LipschitzOnWith.of_le_add

protected theorem le_add_mul {f : Œ± ‚Üí ‚Ñù} {K : ‚Ñù‚â•0} (h : LipschitzOnWith K f s) {x : Œ±} (hx : x ‚àà s)
    {y : Œ±} (hy : y ‚àà s) : f x ‚â§ f y + K * dist x y :=
  sub_le_iff_le_add'.1 <| le_trans (le_abs_self _) <| h.dist_le_mul x hx y hy
#align lipschitz_on_with.le_add_mul LipschitzOnWith.le_add_mul

protected theorem iff_le_add_mul {f : Œ± ‚Üí ‚Ñù} {K : ‚Ñù‚â•0} :
    LipschitzOnWith K f s ‚Üî ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, f x ‚â§ f y + K * dist x y :=
  ‚ü®LipschitzOnWith.le_add_mul, LipschitzOnWith.of_le_add_mul K‚ü©
#align lipschitz_on_with.iff_le_add_mul LipschitzOnWith.iff_le_add_mul

theorem isBounded_image2 (f : Œ± ‚Üí Œ≤ ‚Üí Œ≥) {K‚ÇÅ K‚ÇÇ : ‚Ñù‚â•0} {s : Set Œ±} {t : Set Œ≤}
    (hs : Bornology.IsBounded s) (ht : Bornology.IsBounded t)
    (hf‚ÇÅ : ‚àÄ b ‚àà t, LipschitzOnWith K‚ÇÅ (fun a => f a b) s)
    (hf‚ÇÇ : ‚àÄ a ‚àà s, LipschitzOnWith K‚ÇÇ (f a) t) : Bornology.IsBounded (Set.image2 f s t) :=
  Metric.isBounded_iff_ediam_ne_top.2 <|
    ne_top_of_le_ne_top
      (ENNReal.add_ne_top.mpr
        ‚ü®ENNReal.mul_ne_top ENNReal.coe_ne_top hs.ediam_ne_top,
          ENNReal.mul_ne_top ENNReal.coe_ne_top ht.ediam_ne_top‚ü©)
      (ediam_image2_le _ _ _ hf‚ÇÅ hf‚ÇÇ)
#align lipschitz_on_with.bounded_image2 LipschitzOnWith.isBounded_image2

end Metric

end LipschitzOnWith

namespace LocallyLipschitz
variable [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] [PseudoEMetricSpace Œ≥] {f : Œ± ‚Üí Œ≤}

/-- A Lipschitz function is locally Lipschitz. -/
protected lemma _root_.LipschitzWith.locallyLipschitz {K : ‚Ñù‚â•0} (hf : LipschitzWith K f) :
    LocallyLipschitz f :=
  fun _ ‚Ü¶ ‚ü®K, univ, Filter.univ_mem, lipschitzOn_univ.mpr hf‚ü©

/-- The identity function is locally Lipschitz. -/
protected lemma id : LocallyLipschitz (@id Œ±) := LipschitzWith.id.locallyLipschitz

/-- Constant functions are locally Lipschitz. -/
protected lemma const (b : Œ≤) : LocallyLipschitz (fun _ : Œ± ‚Ü¶ b) :=
  (LipschitzWith.const b).locallyLipschitz

/-- A locally Lipschitz function is continuous. (The converse is false: for example,
$x ‚Ü¶ \sqrt{x}$ is continuous, but not locally Lipschitz at 0.) -/
protected theorem continuous {f : Œ± ‚Üí Œ≤} (hf : LocallyLipschitz f) : Continuous f := by
  apply continuous_iff_continuousAt.mpr
  intro x
  rcases (hf x) with ‚ü®K, t, ht, hK‚ü©
  exact (hK.continuousOn).continuousAt ht

/-- The composition of locally Lipschitz functions is locally Lipschitz. --/
protected lemma comp  {f : Œ≤ ‚Üí Œ≥} {g : Œ± ‚Üí Œ≤}
    (hf : LocallyLipschitz f) (hg : LocallyLipschitz g) : LocallyLipschitz (f ‚àò g) := by
  intro x
  -- g is Lipschitz on t ‚àã x, f is Lipschitz on u ‚àã g(x)
  rcases hg x with ‚ü®Kg, t, ht, hgL‚ü©
  rcases hf (g x) with ‚ü®Kf, u, hu, hfL‚ü©
  refine ‚ü®Kf * Kg, t ‚à© g‚Åª¬π' u, inter_mem ht (hg.continuous.continuousAt hu), ?_‚ü©
  exact hfL.comp (hgL.mono (inter_subset_left _ _))
    ((mapsTo_preimage g u).mono_left (inter_subset_right _ _))

/-- If `f` and `g` are locally Lipschitz, so is the induced map `f √ó g` to the product type. -/
protected lemma prod {f : Œ± ‚Üí Œ≤} (hf : LocallyLipschitz f) {g : Œ± ‚Üí Œ≥} (hg : LocallyLipschitz g) :
    LocallyLipschitz fun x => (f x, g x) := by
  intro x
  rcases hf x with ‚ü®Kf, t‚ÇÅ, h‚ÇÅt, hfL‚ü©
  rcases hg x with ‚ü®Kg, t‚ÇÇ, h‚ÇÇt, hgL‚ü©
  refine ‚ü®max Kf Kg, t‚ÇÅ ‚à© t‚ÇÇ, Filter.inter_mem h‚ÇÅt h‚ÇÇt, ?_‚ü©
  exact (hfL.mono (inter_subset_left t‚ÇÅ t‚ÇÇ)).prod (hgL.mono (inter_subset_right t‚ÇÅ t‚ÇÇ))

protected theorem prod_mk_left (a : Œ±) : LocallyLipschitz (Prod.mk a : Œ≤ ‚Üí Œ± √ó Œ≤) :=
  (LipschitzWith.prod_mk_left a).locallyLipschitz

protected theorem prod_mk_right (b : Œ≤) : LocallyLipschitz (fun a : Œ± => (a, b)) :=
  (LipschitzWith.prod_mk_right b).locallyLipschitz

protected theorem iterate {f : Œ± ‚Üí Œ±} (hf : LocallyLipschitz f) : ‚àÄ n, LocallyLipschitz f^[n]
  | 0 => by simpa only [pow_zero] using LocallyLipschitz.id
  | n + 1 => by rw [iterate_add, iterate_one]; exact (hf.iterate n).comp hf

protected theorem mul_end {f g : Function.End Œ±} (hf : LocallyLipschitz f)
    (hg : LocallyLipschitz g) : LocallyLipschitz (f * g : Function.End Œ±) := hf.comp hg

protected theorem pow_end {f : Function.End Œ±} (h : LocallyLipschitz f) :
    ‚àÄ n : ‚Ñï, LocallyLipschitz (f ^ n : Function.End Œ±)
  | 0 => by simpa only [pow_zero] using LocallyLipschitz.id
  | n + 1 => by
    rw [pow_succ]
    exact h.mul_end (h.pow_end n)

section Real
variable {f g : Œ± ‚Üí ‚Ñù}
/-- The minimum of locally Lipschitz functions is locally Lipschitz. -/
protected lemma min (hf : LocallyLipschitz f) (hg : LocallyLipschitz g) :
    LocallyLipschitz (fun x => min (f x) (g x)) :=
  lipschitzWith_min.locallyLipschitz.comp (hf.prod hg)

/-- The maximum of locally Lipschitz functions is locally Lipschitz. -/
protected lemma max (hf : LocallyLipschitz f) (hg : LocallyLipschitz g) :
    LocallyLipschitz (fun x => max (f x) (g x)) :=
  lipschitzWith_max.locallyLipschitz.comp (hf.prod hg)

theorem max_const (hf : LocallyLipschitz f) (a : ‚Ñù) : LocallyLipschitz fun x => max (f x) a :=
  hf.max (LocallyLipschitz.const a)

theorem const_max (hf : LocallyLipschitz f) (a : ‚Ñù) : LocallyLipschitz fun x => max a (f x) := by
  simpa [max_comm] using (hf.max_const a)

theorem min_const (hf : LocallyLipschitz f) (a : ‚Ñù) : LocallyLipschitz fun x => min (f x) a :=
  hf.min (LocallyLipschitz.const a)

theorem const_min (hf : LocallyLipschitz f) (a : ‚Ñù) : LocallyLipschitz fun x => min a (f x) := by
  simpa [min_comm] using (hf.min_const a)

end Real
end LocallyLipschitz

/-- Consider a function `f : Œ± √ó Œ≤ ‚Üí Œ≥`. Suppose that it is continuous on each ‚Äúvertical fiber‚Äù
`{a} √ó t`, `a ‚àà s`, and is Lipschitz continuous on each ‚Äúhorizontal fiber‚Äù `s √ó {b}`, `b ‚àà t`
with the same Lipschitz constant `K`. Then it is continuous on `s √ó t`. Moreover, it suffices
to require continuity on vertical fibers for `a` from a subset `s' ‚äÜ s` that is dense in `s`.

The actual statement uses (Lipschitz) continuity of `fun y ‚Ü¶ f (a, y)` and `fun x ‚Ü¶ f (x, b)`
instead of continuity of `f` on subsets of the product space. -/
theorem continuousOn_prod_of_subset_closure_continuousOn_lipschitzOnWith [PseudoEMetricSpace Œ±]
    [TopologicalSpace Œ≤] [PseudoEMetricSpace Œ≥] (f : Œ± √ó Œ≤ ‚Üí Œ≥) {s s' : Set Œ±} {t : Set Œ≤}
    (hs' : s' ‚äÜ s) (hss' : s ‚äÜ closure s') (K : ‚Ñù‚â•0)
    (ha : ‚àÄ a ‚àà s', ContinuousOn (fun y => f (a, y)) t)
    (hb : ‚àÄ b ‚àà t, LipschitzOnWith K (fun x => f (x, b)) s) : ContinuousOn f (s √óÀ¢ t) := by
  rintro ‚ü®x, y‚ü© ‚ü®hx : x ‚àà s, hy : y ‚àà t‚ü©
  refine' EMetric.nhds_basis_closed_eball.tendsto_right_iff.2 fun Œµ (Œµ0 : 0 < Œµ) => _
  replace Œµ0 : 0 < Œµ / 2 := ENNReal.half_pos Œµ0.ne'
  obtain ‚ü®Œ¥, Œ¥pos, hŒ¥‚ü© : ‚àÉ Œ¥ : ‚Ñù‚â•0, 0 < Œ¥ ‚àß (Œ¥ : ‚Ñù‚â•0‚àû) * ‚Üë(3 * K) < Œµ / 2 :=
    ENNReal.exists_nnreal_pos_mul_lt ENNReal.coe_ne_top Œµ0.ne'
  rw [‚Üê ENNReal.coe_pos] at Œ¥pos
  rcases EMetric.mem_closure_iff.1 (hss' hx) Œ¥ Œ¥pos with ‚ü®x', hx', hxx'‚ü©
  have A : s ‚à© EMetric.ball x Œ¥ ‚àà ùìù[s] x :=
    inter_mem_nhdsWithin _ (EMetric.ball_mem_nhds _ Œ¥pos)
  have B : t ‚à© { b | edist (f (x', b)) (f (x', y)) ‚â§ Œµ / 2 } ‚àà ùìù[t] y :=
    inter_mem self_mem_nhdsWithin (ha x' hx' y hy (EMetric.closedBall_mem_nhds (f (x', y)) Œµ0))
  filter_upwards [nhdsWithin_prod A B] with ‚ü®a, b‚ü© ‚ü®‚ü®has, hax‚ü©, ‚ü®hbt, hby‚ü©‚ü©
  calc
    edist (f (a, b)) (f (x, y)) ‚â§ edist (f (a, b)) (f (x', b)) + edist (f (x', b)) (f (x', y)) +
        edist (f (x', y)) (f (x, y)) := edist_triangle4 _ _ _ _
    _ ‚â§ K * (Œ¥ + Œ¥) + Œµ / 2 + K * Œ¥ := by
      gcongr
      ¬∑ refine (hb b hbt).edist_le_mul_of_le has (hs' hx') ?_
        refine (edist_triangle _ _ _).trans (add_le_add (le_of_lt hax) hxx'.le)
      ¬∑ exact hby
      ¬∑ exact (hb y hy).edist_le_mul_of_le (hs' hx') hx ((edist_comm _ _).trans_le hxx'.le)
    _ = Œ¥ * ‚Üë(3 * K) + Œµ / 2 := by push_cast; ring
    _ ‚â§ Œµ / 2 + Œµ / 2 := by gcongr
    _ = Œµ := ENNReal.add_halves _

/-- Consider a function `f : Œ± √ó Œ≤ ‚Üí Œ≥`. Suppose that it is continuous on each ‚Äúvertical fiber‚Äù
`{a} √ó t`, `a ‚àà s`, and is Lipschitz continuous on each ‚Äúhorizontal fiber‚Äù `s √ó {b}`, `b ‚àà t`
with the same Lipschitz constant `K`. Then it is continuous on `s √ó t`.

The actual statement uses (Lipschitz) continuity of `fun y ‚Ü¶ f (a, y)` and `fun x ‚Ü¶ f (x, b)`
instead of continuity of `f` on subsets of the product space. -/
theorem continuousOn_prod_of_continuousOn_lipschitzOnWith [PseudoEMetricSpace Œ±]
    [TopologicalSpace Œ≤] [PseudoEMetricSpace Œ≥] (f : Œ± √ó Œ≤ ‚Üí Œ≥) {s : Set Œ±} {t : Set Œ≤} (K : ‚Ñù‚â•0)
    (ha : ‚àÄ a ‚àà s, ContinuousOn (fun y => f (a, y)) t)
    (hb : ‚àÄ b ‚àà t, LipschitzOnWith K (fun x => f (x, b)) s) : ContinuousOn f (s √óÀ¢ t) :=
  continuousOn_prod_of_subset_closure_continuousOn_lipschitzOnWith
    f Subset.rfl subset_closure K ha hb
#align continuous_on_prod_of_continuous_on_lipschitz_on continuousOn_prod_of_continuousOn_lipschitzOnWith

/-- Consider a function `f : Œ± √ó Œ≤ ‚Üí Œ≥`. Suppose that it is continuous on each ‚Äúvertical section‚Äù
`{a} √ó univ` for `a : Œ±` from a dense set. Suppose that it is Lipschitz continuous on each
‚Äúhorizontal section‚Äù `univ √ó {b}`, `b : Œ≤` with the same Lipschitz constant `K`. Then it is
continuous.

The actual statement uses (Lipschitz) continuity of `fun y ‚Ü¶ f (a, y)` and `fun x ‚Ü¶ f (x, b)`
instead of continuity of `f` on subsets of the product space. -/
theorem continuous_prod_of_dense_continuous_lipschitzWith [PseudoEMetricSpace Œ±]
    [TopologicalSpace Œ≤] [PseudoEMetricSpace Œ≥] (f : Œ± √ó Œ≤ ‚Üí Œ≥) (K : ‚Ñù‚â•0) {s : Set Œ±}
    (hs : Dense s) (ha : ‚àÄ a ‚àà s, Continuous fun y => f (a, y))
    (hb : ‚àÄ b, LipschitzWith K fun x => f (x, b)) : Continuous f := by
  simp only [continuous_iff_continuousOn_univ, ‚Üê univ_prod_univ, ‚Üê lipschitzOn_univ] at *
  exact continuousOn_prod_of_subset_closure_continuousOn_lipschitzOnWith f (subset_univ _)
    hs.closure_eq.ge K ha fun b _ => hb b

/-- Consider a function `f : Œ± √ó Œ≤ ‚Üí Œ≥`. Suppose that it is continuous on each ‚Äúvertical section‚Äù
`{a} √ó univ`, `a : Œ±`, and is Lipschitz continuous on each ‚Äúhorizontal section‚Äù
`univ √ó {b}`, `b : Œ≤` with the same Lipschitz constant `K`. Then it is continuous.

The actual statement uses (Lipschitz) continuity of `fun y ‚Ü¶ f (a, y)` and `fun x ‚Ü¶ f (x, b)`
instead of continuity of `f` on subsets of the product space. -/
theorem continuous_prod_of_continuous_lipschitzWith [PseudoEMetricSpace Œ±] [TopologicalSpace Œ≤]
    [PseudoEMetricSpace Œ≥] (f : Œ± √ó Œ≤ ‚Üí Œ≥) (K : ‚Ñù‚â•0) (ha : ‚àÄ a, Continuous fun y => f (a, y))
    (hb : ‚àÄ b, LipschitzWith K fun x => f (x, b)) : Continuous f :=
  continuous_prod_of_dense_continuous_lipschitzWith f K dense_univ (fun _ _ ‚Ü¶ ha _) hb
#align continuous_prod_of_continuous_lipschitz continuous_prod_of_continuous_lipschitzWith

open Metric

/-- If a function is locally Lipschitz around a point, then it is continuous at this point. -/
theorem continuousAt_of_locally_lipschitz [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤}
    {x : Œ±} {r : ‚Ñù} (hr : 0 < r) (K : ‚Ñù) (h : ‚àÄ y, dist y x < r ‚Üí dist (f y) (f x) ‚â§ K * dist y x) :
    ContinuousAt f x := by
  -- We use `h` to squeeze `dist (f y) (f x)` between `0` and `K * dist y x`
  refine tendsto_iff_dist_tendsto_zero.2 (squeeze_zero' (eventually_of_forall fun _ => dist_nonneg)
    (mem_of_superset (ball_mem_nhds _ hr) h) ?_)
  -- Then show that `K * dist y x` tends to zero as `y ‚Üí x`
  refine (continuous_const.mul (continuous_id.dist continuous_const)).tendsto' _ _ ?_
  simp
#align continuous_at_of_locally_lipschitz continuousAt_of_locally_lipschitz

/-- A function `f : Œ± ‚Üí ‚Ñù` which is `K`-Lipschitz on a subset `s` admits a `K`-Lipschitz extension
to the whole space. -/
theorem LipschitzOnWith.extend_real [PseudoMetricSpace Œ±] {f : Œ± ‚Üí ‚Ñù} {s : Set Œ±} {K : ‚Ñù‚â•0}
    (hf : LipschitzOnWith K f s) : ‚àÉ g : Œ± ‚Üí ‚Ñù, LipschitzWith K g ‚àß EqOn f g s := by
  /- An extension is given by `g y = Inf {f x + K * dist y x | x ‚àà s}`. Taking `x = y`, one has
    `g y ‚â§ f y` for `y ‚àà s`, and the other inequality holds because `f` is `K`-Lipschitz, so that it
    can not counterbalance the growth of `K * dist y x`. One readily checks from the formula that
    the extended function is also `K`-Lipschitz. -/
  rcases eq_empty_or_nonempty s with (rfl | hs)
  ¬∑ exact ‚ü®fun _ => 0, (LipschitzWith.const _).weaken (zero_le _), eqOn_empty _ _‚ü©
  have : Nonempty s := by simp only [hs, nonempty_coe_sort]
  let g := fun y : Œ± => iInf fun x : s => f x + K * dist y x
  have B : ‚àÄ y : Œ±, BddBelow (range fun x : s => f x + K * dist y x) := fun y => by
    rcases hs with ‚ü®z, hz‚ü©
    refine' ‚ü®f z - K * dist y z, _‚ü©
    rintro w ‚ü®t, rfl‚ü©
    dsimp
    rw [sub_le_iff_le_add, add_assoc, ‚Üê mul_add, add_comm (dist y t)]
    calc
      f z ‚â§ f t + K * dist z t := hf.le_add_mul hz t.2
      _ ‚â§ f t + K * (dist y z + dist y t) := by gcongr; apply dist_triangle_left
  have E : EqOn f g s := fun x hx => by
    refine' le_antisymm (le_ciInf fun y => hf.le_add_mul hx y.2) _
    simpa only [add_zero, Subtype.coe_mk, mul_zero, dist_self] using ciInf_le (B x) ‚ü®x, hx‚ü©
  refine' ‚ü®g, LipschitzWith.of_le_add_mul K fun x y => _, E‚ü©
  rw [‚Üê sub_le_iff_le_add]
  refine' le_ciInf fun z => _
  rw [sub_le_iff_le_add]
  calc
    g x ‚â§ f z + K * dist x z := ciInf_le (B x) _
    _ ‚â§ f z + K * dist y z + K * dist x y := by
      rw [add_assoc, ‚Üê mul_add, add_comm (dist y z)]
      gcongr
      apply dist_triangle
#align lipschitz_on_with.extend_real LipschitzOnWith.extend_real

/-- A function `f : Œ± ‚Üí (Œπ ‚Üí ‚Ñù)` which is `K`-Lipschitz on a subset `s` admits a `K`-Lipschitz
extension to the whole space. The same result for the space `‚Ñì^‚àû (Œπ, ‚Ñù)` over a possibly infinite
type `Œπ` is implemented in `LipschitzOnWith.extend_lp_infty`.-/
theorem LipschitzOnWith.extend_pi [PseudoMetricSpace Œ±] [Fintype Œπ] {f : Œ± ‚Üí Œπ ‚Üí ‚Ñù} {s : Set Œ±}
    {K : ‚Ñù‚â•0} (hf : LipschitzOnWith K f s) : ‚àÉ g : Œ± ‚Üí Œπ ‚Üí ‚Ñù, LipschitzWith K g ‚àß EqOn f g s := by
  have : ‚àÄ i, ‚àÉ g : Œ± ‚Üí ‚Ñù, LipschitzWith K g ‚àß EqOn (fun x => f x i) g s := fun i => by
    have : LipschitzOnWith K (fun x : Œ± => f x i) s :=
      LipschitzOnWith.of_dist_le_mul fun x hx y hy =>
        (dist_le_pi_dist _ _ i).trans (hf.dist_le_mul x hx y hy)
    exact this.extend_real
  choose g hg using this
  refine' ‚ü®fun x i => g i x, LipschitzWith.of_dist_le_mul fun x y => _, _‚ü©
  ¬∑ exact (dist_pi_le_iff (mul_nonneg K.2 dist_nonneg)).2 fun i => (hg i).1.dist_le_mul x y
  ¬∑ intro x hx
    ext1 i
    exact (hg i).2 hx
#align lipschitz_on_with.extend_pi LipschitzOnWith.extend_pi
