/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Topology.MetricSpace.Lipschitz
import Mathlib.Analysis.SpecialFunctions.Pow.Continuity

/-!
# H√∂lder continuous functions

In this file we define H√∂lder continuity on a set and on the whole space. We also prove some basic
properties of H√∂lder continuous functions.

## Main definitions

* `HoelderOnWith`: `f : X ‚Üí Y` is said to be *H√∂lder continuous* with constant `C : ‚Ñù‚â•0` and
  exponent `r : ‚Ñù‚â•0` on a set `s`, if `edist (f x) (f y) ‚â§ C * edist x y ^ r` for all `x y ‚àà s`;
* `HoelderWith`: `f : X ‚Üí Y` is said to be *H√∂lder continuous* with constant `C : ‚Ñù‚â•0` and exponent
  `r : ‚Ñù‚â•0`, if `edist (f x) (f y) ‚â§ C * edist x y ^ r` for all `x y : X`.

## Implementation notes

We use the type `‚Ñù‚â•0` (a.k.a. `NNReal`) for `C` because this type has coercion both to `‚Ñù` and
`‚Ñù‚â•0‚àû`, so it can be easily used both in inequalities about `dist` and `edist`. We also use `‚Ñù‚â•0`
for `r` to ensure that `d ^ r` is monotone in `d`. It might be a good idea to use
`‚Ñù>0` for `r` but we don't have this type in `mathlib` (yet).

## Tags

H√∂lder continuity, Lipschitz continuity

 -/


variable {X Y Z : Type*}

open Filter Set

open NNReal ENNReal Topology

section Emetric

variable [PseudoEMetricSpace X] [PseudoEMetricSpace Y] [PseudoEMetricSpace Z] {C r : ‚Ñù‚â•0}
  {f : X ‚Üí Y}

/-- A function `f : X ‚Üí Y` between two `PseudoEMetricSpace`s is H√∂lder continuous with constant
`C : ‚Ñù‚â•0` and exponent `r : ‚Ñù‚â•0`, if `edist (f x) (f y) ‚â§ C * edist x y ^ r` for all `x y : X`. -/
def HoelderWith (C r : ‚Ñù‚â•0) (f : X ‚Üí Y) : Prop :=
  ‚àÄ x y, edist (f x) (f y) ‚â§ (C : ‚Ñù‚â•0‚àû) * edist x y ^ (r : ‚Ñù)

/-- A function `f : X ‚Üí Y` between two `PseudoEMetricSpace`s is H√∂lder continuous with constant
`C : ‚Ñù‚â•0` and exponent `r : ‚Ñù‚â•0` on a set `s : Set X`, if `edist (f x) (f y) ‚â§ C * edist x y ^ r`
for all `x y ‚àà s`. -/
def HoelderOnWith (C r : ‚Ñù‚â•0) (f : X ‚Üí Y) (s : Set X) : Prop :=
  ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, edist (f x) (f y) ‚â§ (C : ‚Ñù‚â•0‚àû) * edist x y ^ (r : ‚Ñù)

@[simp]
theorem hoelderOnWith_empty (C r : ‚Ñù‚â•0) (f : X ‚Üí Y) : HoelderOnWith C r f ‚àÖ := fun _ hx => hx.elim

@[simp]
theorem hoelderOnWith_singleton (C r : ‚Ñù‚â•0) (f : X ‚Üí Y) (x : X) : HoelderOnWith C r f {x} := by
  rintro a (rfl : a = x) b (rfl : b = a)
  rw [edist_self]
  exact zero_le _

theorem Set.Subsingleton.HoelderOnWith {s : Set X} (hs : s.Subsingleton) (C r : ‚Ñù‚â•0) (f : X ‚Üí Y) :
    HoelderOnWith C r f s :=
  hs.induction_on (hoelderOnWith_empty C r f) (hoelderOnWith_singleton C r f)

lemma hoelderOnWith_univ : HoelderOnWith C r f univ ‚Üî HoelderWith C r f := by
  simp only [HoelderOnWith, HoelderWith, mem_univ, true_imp_iff]

@[simp]
theorem hoelderOnWith_one {C : ‚Ñù‚â•0} {f : X ‚Üí Y} {s : Set X} :
    HoelderOnWith C 1 f s ‚Üî LipschitzOnWith C f s := by
  simp only [HoelderOnWith, LipschitzOnWith, NNReal.coe_one, ENNReal.rpow_one]

alias ‚ü®_, LipschitzOnWith.HoelderOnWith‚ü© := hoelderOnWith_one

@[simp]
theorem hoelderWith_one {C : ‚Ñù‚â•0} {f : X ‚Üí Y} : HoelderWith C 1 f ‚Üî LipschitzWith C f :=
  hoelderOnWith_univ.symm.trans <| hoelderOnWith_one.trans lipschitzOnWith_univ

alias ‚ü®_, LipschitzWith.HoelderWith‚ü© := hoelderWith_one

theorem hoelderWith_id : HoelderWith 1 1 (id : X ‚Üí X) :=
  LipschitzWith.id.HoelderWith

protected theorem HoelderWith.HoelderOnWith {C r : ‚Ñù‚â•0} {f : X ‚Üí Y} (h : HoelderWith C r f)
    (s : Set X) : HoelderOnWith C r f s := fun x _ y _ => h x y

namespace HoelderOnWith

variable {C r : ‚Ñù‚â•0} {f : X ‚Üí Y} {s t : Set X}

theorem edist_le (h : HoelderOnWith C r f s) {x y : X} (hx : x ‚àà s) (hy : y ‚àà s) :
    edist (f x) (f y) ‚â§ (C : ‚Ñù‚â•0‚àû) * edist x y ^ (r : ‚Ñù) :=
  h x hx y hy

theorem edist_le_of_le (h : HoelderOnWith C r f s) {x y : X} (hx : x ‚àà s) (hy : y ‚àà s) {d : ‚Ñù‚â•0‚àû}
    (hd : edist x y ‚â§ d) : edist (f x) (f y) ‚â§ (C : ‚Ñù‚â•0‚àû) * d ^ (r : ‚Ñù) :=
  (h.edist_le hx hy).trans <| by gcongr

theorem comp {Cg rg : ‚Ñù‚â•0} {g : Y ‚Üí Z} {t : Set Y} (hg : HoelderOnWith Cg rg g t) {Cf rf : ‚Ñù‚â•0}
    {f : X ‚Üí Y} (hf : HoelderOnWith Cf rf f s) (hst : MapsTo f s t) :
    HoelderOnWith (Cg * Cf ^ (rg : ‚Ñù)) (rg * rf) (g ‚àò f) s := by
  intro x hx y hy
  rw [ENNReal.coe_mul, mul_comm rg, NNReal.coe_mul, ENNReal.rpow_mul, mul_assoc,
    ENNReal.coe_rpow_of_nonneg _ rg.coe_nonneg, ‚Üê ENNReal.mul_rpow_of_nonneg _ _ rg.coe_nonneg]
  exact hg.edist_le_of_le (hst hx) (hst hy) (hf.edist_le hx hy)

theorem comp_hoelderWith {Cg rg : ‚Ñù‚â•0} {g : Y ‚Üí Z} {t : Set Y} (hg : HoelderOnWith Cg rg g t)
    {Cf rf : ‚Ñù‚â•0} {f : X ‚Üí Y} (hf : HoelderWith Cf rf f) (ht : ‚àÄ x, f x ‚àà t) :
    HoelderWith (Cg * Cf ^ (rg : ‚Ñù)) (rg * rf) (g ‚àò f) :=
  hoelderOnWith_univ.mp <| hg.comp (hf.HoelderOnWith univ) fun x _ => ht x

/-- A H√∂lder continuous function is uniformly continuous -/
protected theorem uniformContinuousOn (hf : HoelderOnWith C r f s) (h0 : 0 < r) :
    UniformContinuousOn f s := by
  refine EMetric.uniformContinuousOn_iff.2 fun Œµ Œµpos => ?_
  have : Tendsto (fun d : ‚Ñù‚â•0‚àû => (C : ‚Ñù‚â•0‚àû) * d ^ (r : ‚Ñù)) (ùìù 0) (ùìù 0) :=
    ENNReal.tendsto_const_mul_rpow_nhds_zero_of_pos ENNReal.coe_ne_top h0
  rcases ENNReal.nhds_zero_basis.mem_iff.1 (this (gt_mem_nhds Œµpos)) with ‚ü®Œ¥, Œ¥0, H‚ü©
  exact ‚ü®Œ¥, Œ¥0, fun hx y hy h => (hf.edist_le hx hy).trans_lt (H h)‚ü©

protected theorem continuousOn (hf : HoelderOnWith C r f s) (h0 : 0 < r) : ContinuousOn f s :=
  (hf.uniformContinuousOn h0).continuousOn

protected theorem mono (hf : HoelderOnWith C r f s) (ht : t ‚äÜ s) : HoelderOnWith C r f t :=
  fun _ hx _ hy => hf.edist_le (ht hx) (ht hy)

theorem ediam_image_le_of_le (hf : HoelderOnWith C r f s) {d : ‚Ñù‚â•0‚àû} (hd : EMetric.diam s ‚â§ d) :
    EMetric.diam (f '' s) ‚â§ (C : ‚Ñù‚â•0‚àû) * d ^ (r : ‚Ñù) :=
  EMetric.diam_image_le_iff.2 fun _ hx _ hy =>
    hf.edist_le_of_le hx hy <| (EMetric.edist_le_diam_of_mem hx hy).trans hd

theorem ediam_image_le (hf : HoelderOnWith C r f s) :
    EMetric.diam (f '' s) ‚â§ (C : ‚Ñù‚â•0‚àû) * EMetric.diam s ^ (r : ‚Ñù) :=
  hf.ediam_image_le_of_le le_rfl

theorem ediam_image_le_of_subset (hf : HoelderOnWith C r f s) (ht : t ‚äÜ s) :
    EMetric.diam (f '' t) ‚â§ (C : ‚Ñù‚â•0‚àû) * EMetric.diam t ^ (r : ‚Ñù) :=
  (hf.mono ht).ediam_image_le

theorem ediam_image_le_of_subset_of_le (hf : HoelderOnWith C r f s) (ht : t ‚äÜ s) {d : ‚Ñù‚â•0‚àû}
    (hd : EMetric.diam t ‚â§ d) : EMetric.diam (f '' t) ‚â§ (C : ‚Ñù‚â•0‚àû) * d ^ (r : ‚Ñù) :=
  (hf.mono ht).ediam_image_le_of_le hd

theorem ediam_image_inter_le_of_le (hf : HoelderOnWith C r f s) {d : ‚Ñù‚â•0‚àû}
    (hd : EMetric.diam t ‚â§ d) : EMetric.diam (f '' (t ‚à© s)) ‚â§ (C : ‚Ñù‚â•0‚àû) * d ^ (r : ‚Ñù) :=
  hf.ediam_image_le_of_subset_of_le inter_subset_right <|
    (EMetric.diam_mono inter_subset_left).trans hd

theorem ediam_image_inter_le (hf : HoelderOnWith C r f s) (t : Set X) :
    EMetric.diam (f '' (t ‚à© s)) ‚â§ (C : ‚Ñù‚â•0‚àû) * EMetric.diam t ^ (r : ‚Ñù) :=
  hf.ediam_image_inter_le_of_le le_rfl

end HoelderOnWith

namespace HoelderWith

variable {C r : ‚Ñù‚â•0} {f : X ‚Üí Y}

theorem edist_le (h : HoelderWith C r f) (x y : X) :
    edist (f x) (f y) ‚â§ (C : ‚Ñù‚â•0‚àû) * edist x y ^ (r : ‚Ñù) :=
  h x y

theorem edist_le_of_le (h : HoelderWith C r f) {x y : X} {d : ‚Ñù‚â•0‚àû} (hd : edist x y ‚â§ d) :
    edist (f x) (f y) ‚â§ (C : ‚Ñù‚â•0‚àû) * d ^ (r : ‚Ñù) :=
  (h.HoelderOnWith univ).edist_le_of_le trivial trivial hd

theorem comp {Cg rg : ‚Ñù‚â•0} {g : Y ‚Üí Z} (hg : HoelderWith Cg rg g) {Cf rf : ‚Ñù‚â•0} {f : X ‚Üí Y}
    (hf : HoelderWith Cf rf f) : HoelderWith (Cg * Cf ^ (rg : ‚Ñù)) (rg * rf) (g ‚àò f) :=
  (hg.HoelderOnWith univ).comp_hoelderWith hf fun _ => trivial

theorem comp_hoelderOnWith {Cg rg : ‚Ñù‚â•0} {g : Y ‚Üí Z} (hg : HoelderWith Cg rg g) {Cf rf : ‚Ñù‚â•0}
    {f : X ‚Üí Y} {s : Set X} (hf : HoelderOnWith Cf rf f s) :
    HoelderOnWith (Cg * Cf ^ (rg : ‚Ñù)) (rg * rf) (g ‚àò f) s :=
  (hg.HoelderOnWith univ).comp hf fun _ _ => trivial

/-- A H√∂lder continuous function is uniformly continuous -/
protected theorem uniformContinuous (hf : HoelderWith C r f) (h0 : 0 < r) : UniformContinuous f :=
  uniformContinuousOn_univ.mp <| (hf.HoelderOnWith univ).uniformContinuousOn h0

protected theorem continuous (hf : HoelderWith C r f) (h0 : 0 < r) : Continuous f :=
  (hf.uniformContinuous h0).continuous

theorem ediam_image_le (hf : HoelderWith C r f) (s : Set X) :
    EMetric.diam (f '' s) ‚â§ (C : ‚Ñù‚â•0‚àû) * EMetric.diam s ^ (r : ‚Ñù) :=
  EMetric.diam_image_le_iff.2 fun _ hx _ hy =>
    hf.edist_le_of_le <| EMetric.edist_le_diam_of_mem hx hy

lemma const {y : Y} :
    HoelderWith C r (Function.const X y) := fun x‚ÇÅ x‚ÇÇ => by
  simp only [Function.const_apply, edist_self, zero_le]

lemma zero [Zero Y] : HoelderWith C r (0 : X ‚Üí Y) := .const

lemma of_isEmpty [IsEmpty X] : HoelderWith C r f := isEmptyElim

lemma mono {C' : ‚Ñù‚â•0} (hf : HoelderWith C r f) (h : C ‚â§ C') :
    HoelderWith C' r f :=
  fun x‚ÇÅ x‚ÇÇ ‚Ü¶ (hf x‚ÇÅ x‚ÇÇ).trans (mul_right_mono (coe_le_coe.2 h))

end HoelderWith

end Emetric

section PseudoMetric

variable [PseudoMetricSpace X] [PseudoMetricSpace Y] {C r : ‚Ñù‚â•0} {f : X ‚Üí Y}

namespace HoelderWith

theorem nndist_le_of_le (hf : HoelderWith C r f) {x y : X} {d : ‚Ñù‚â•0} (hd : nndist x y ‚â§ d) :
    nndist (f x) (f y) ‚â§ C * d ^ (r : ‚Ñù) := by
  rw [‚Üê ENNReal.coe_le_coe, ‚Üê edist_nndist, ENNReal.coe_mul,
    ENNReal.coe_rpow_of_nonneg _ r.coe_nonneg]
  apply hf.edist_le_of_le
  rwa [edist_nndist, ENNReal.coe_le_coe]

theorem nndist_le (hf : HoelderWith C r f) (x y : X) :
    nndist (f x) (f y) ‚â§ C * nndist x y ^ (r : ‚Ñù) :=
  hf.nndist_le_of_le le_rfl

theorem dist_le_of_le (hf : HoelderWith C r f) {x y : X} {d : ‚Ñù} (hd : dist x y ‚â§ d) :
    dist (f x) (f y) ‚â§ C * d ^ (r : ‚Ñù) := by
  lift d to ‚Ñù‚â•0 using dist_nonneg.trans hd
  rw [dist_nndist] at hd ‚ä¢
  norm_cast at hd ‚ä¢
  exact hf.nndist_le_of_le hd

theorem dist_le (hf : HoelderWith C r f) (x y : X) : dist (f x) (f y) ‚â§ C * dist x y ^ (r : ‚Ñù) :=
  hf.dist_le_of_le le_rfl

end HoelderWith

end PseudoMetric

section Metric

variable [PseudoMetricSpace X] [MetricSpace Y] {C r : ‚Ñù‚â•0} {f : X ‚Üí Y}

@[simp]
lemma hoelderWith_zero_iff : HoelderWith 0 r f ‚Üî ‚àÄ x‚ÇÅ x‚ÇÇ, f x‚ÇÅ = f x‚ÇÇ := by
  refine ‚ü®fun h x‚ÇÅ x‚ÇÇ => ?_, fun h x‚ÇÅ x‚ÇÇ => h x‚ÇÅ x‚ÇÇ ‚ñ∏ ?_‚ü©
  ¬∑ specialize h x‚ÇÅ x‚ÇÇ
    simp [ENNReal.coe_zero, zero_mul, nonpos_iff_eq_zero, edist_eq_zero] at h
    assumption
  ¬∑ simp only [edist_self, ENNReal.coe_zero, zero_mul, le_refl]

end Metric

section SeminormedAddCommGroup

variable [PseudoMetricSpace X] [SeminormedAddCommGroup Y] {C C' r : ‚Ñù‚â•0} {f g : X ‚Üí Y}

namespace HoelderWith

lemma add (hf : HoelderWith C r f) (hg : HoelderWith C' r g) :
    HoelderWith (C + C') r (f + g) := fun x‚ÇÅ x‚ÇÇ => by
  refine le_trans (edist_add_add_le _ _ _ _) <| le_trans (add_le_add (hf x‚ÇÅ x‚ÇÇ) (hg x‚ÇÅ x‚ÇÇ)) ?_
  rw [coe_add, add_mul]

lemma smul {Œ±} [NormedDivisionRing Œ±] [Module Œ± Y] [BoundedSMul Œ± Y] (a : Œ±)
    (hf : HoelderWith C r f) : HoelderWith (C * ‚Äña‚Äñ‚Çä) r (a ‚Ä¢ f) := fun x‚ÇÅ x‚ÇÇ => by
  rw [Pi.smul_apply, coe_mul, Pi.smul_apply, edist_smul‚ÇÄ, mul_comm (C : ‚Ñù‚â•0‚àû),
    ENNReal.smul_def, smul_eq_mul, mul_assoc]
  gcongr
  exact hf x‚ÇÅ x‚ÇÇ

end HoelderWith

end SeminormedAddCommGroup
