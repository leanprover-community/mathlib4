/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
module

public import Mathlib.Analysis.SpecificLimits.Basic
public import Mathlib.Topology.MetricSpace.HausdorffDistance
public import Mathlib.Topology.UniformSpace.Closeds

/-!
# Closed subsets

This file defines the metric and emetric space structure on the types of closed subsets and nonempty
compact subsets of a metric or emetric space.

The Hausdorff distance induces an emetric space structure on the type of closed subsets
of an emetric space, called `Closeds`. Its completeness, resp. compactness, resp.
second-countability, follow from the corresponding properties of the original space.

In a metric space, the type of nonempty compact subsets (called `NonemptyCompacts`) also
inherits a metric space structure from the Hausdorff distance, as the Hausdorff edistance is
always finite in this context.
-/

@[expose] public section

noncomputable section

open Set Function TopologicalSpace Filter Topology ENNReal

namespace Metric

variable {Œ± : Type*} [PseudoEMetricSpace Œ±]

theorem mem_hausdorffEntourage_of_hausdorffEDist_lt {s t : Set Œ±} {Œ¥ : ‚Ñù‚â•0‚àû}
    (h : hausdorffEDist s t < Œ¥) : (s, t) ‚àà hausdorffEntourage {p | edist p.1 p.2 < Œ¥} := by
  rw [hausdorffEDist, max_lt_iff] at h
  rw [hausdorffEntourage, Set.mem_setOf]
  conv => enter [2, 2, 1, 1, _]; rw [edist_comm]
  have {s t : Set Œ±} (h : ‚®Ü x ‚àà s, infEDist x t < Œ¥) :
      s ‚äÜ SetRel.preimage {p | edist p.1 p.2 < Œ¥} t := by
    intro x hx
    simpa only [infEDist, iInf_lt_iff, exists_prop] using (le_iSup‚ÇÇ x hx).trans_lt h
  exact ‚ü®this h.1, this h.2‚ü©

theorem hausdorffEDist_le_of_mem_hausdorffEntourage {s t : Set Œ±} {Œ¥ : ‚Ñù‚â•0‚àû}
    (h : (s, t) ‚àà hausdorffEntourage {p | edist p.1 p.2 ‚â§ Œ¥}) : hausdorffEDist s t ‚â§ Œ¥ := by
  rw [hausdorffEDist, max_le_iff]
  rw [hausdorffEntourage, Set.mem_setOf] at h
  conv at h => enter [2, 2, 1, 1, _]; rw [edist_comm]
  have {s t : Set Œ±} (h : s ‚äÜ SetRel.preimage {p | edist p.1 p.2 ‚â§ Œ¥} t) :
      ‚®Ü x ‚àà s, infEDist x t ‚â§ Œ¥ := by
    rw [iSup‚ÇÇ_le_iff]
    intro x hx
    obtain ‚ü®y, hy, hxy‚ü© := h hx
    exact iInf‚ÇÇ_le_of_le y hy hxy
  exact ‚ü®this h.1, this h.2‚ü©

/-- The Hausdorff pseudo emetric on the powerset of a pseudo emetric space.
See note [reducible non-instances]. -/
protected abbrev _root_.PseudoEMetricSpace.hausdorff : PseudoEMetricSpace (Set Œ±) where
  edist s t := hausdorffEDist s t
  edist_self _ := hausdorffEDist_self
  edist_comm _ _ := hausdorffEDist_comm
  edist_triangle _ _ _ := hausdorffEDist_triangle
  toUniformSpace := .hausdorff Œ±
  uniformity_edist := by
    refine le_antisymm
      (le_iInf‚ÇÇ fun Œµ hŒµ => Filter.le_principal_iff.mpr ?_)
      (uniformity_basis_edist.lift' monotone_hausdorffEntourage |>.ge_iff.mpr fun Œµ hŒµ =>
        Filter.mem_iInf_of_mem Œµ <| Filter.mem_iInf_of_mem hŒµ fun _ =>
        mem_hausdorffEntourage_of_hausdorffEDist_lt)
    obtain ‚ü®Œ¥, hŒ¥, hŒ¥Œµ‚ü© := exists_between hŒµ
    filter_upwards [Filter.mem_lift' (uniformity_basis_edist_le.mem_of_mem hŒ¥)]
      with _ h using hŒ¥Œµ.trans_le' <| hausdorffEDist_le_of_mem_hausdorffEntourage h

end Metric

namespace TopologicalSpace

open Metric

variable {Œ± Œ≤ : Type*} [EMetricSpace Œ±] [EMetricSpace Œ≤] {s : Set Œ±}

namespace Closeds

/-- In emetric spaces, the Hausdorff edistance defines an emetric space structure
on the type of closed subsets -/
instance instEMetricSpace : EMetricSpace (Closeds Œ±) where
  __ := PseudoEMetricSpace.hausdorff.induced SetLike.coe
  eq_of_edist_eq_zero {s t} h := Closeds.ext <| (s.isClosed.hausdorffEDist_zero_iff t.isClosed).1 h

/-- The edistance to a closed set depends continuously on the point and the set -/
theorem continuous_infEDist :
    Continuous fun p : Œ± √ó Closeds Œ± => infEDist p.1 p.2 := by
  refine continuous_of_le_add_edist 2 (by simp) ?_
  rintro ‚ü®x, s‚ü© ‚ü®y, t‚ü©
  calc
    infEDist x s ‚â§ infEDist x t + hausdorffEDist (t : Set Œ±) s :=
      infEDist_le_infEDist_add_hausdorffEDist
    _ ‚â§ infEDist y t + edist x y + hausdorffEDist (t : Set Œ±) s := by
      gcongr; apply infEDist_le_infEDist_add_edist
    _ = infEDist y t + (edist x y + hausdorffEDist (s : Set Œ±) t) := by
      rw [add_assoc, hausdorffEDist_comm]
    _ ‚â§ infEDist y t + (edist (x, s) (y, t) + edist (x, s) (y, t)) := by
      gcongr <;> apply_rules [le_max_left, le_max_right]
    _ = infEDist y t + 2 * edist (x, s) (y, t) := by rw [‚Üê mul_two, mul_comm]

/-- By definition, the edistance on `Closeds Œ±` is given by the Hausdorff edistance -/
theorem edist_eq {s t : Closeds Œ±} : edist s t = hausdorffEDist (s : Set Œ±) t :=
  rfl

/-- In a complete space, the type of closed subsets is complete for the Hausdorff edistance. -/
instance instCompleteSpace [CompleteSpace Œ±] : CompleteSpace (Closeds Œ±) := by
  /- We will show that, if a sequence of sets `s n` satisfies
    `edist (s n) (s (n+1)) < 2^{-n}`, then it converges. This is enough to guarantee
    completeness, by a standard completeness criterion.
    We use the shorthand `B n = 2^{-n}` in ennreal. -/
  let B : ‚Ñï ‚Üí ‚Ñù‚â•0‚àû := fun n => 2‚Åª¬π ^ n
  have B_pos : ‚àÄ n, (0 : ‚Ñù‚â•0‚àû) < B n := by simp [B, ENNReal.pow_pos]
  have B_ne_top : ‚àÄ n, B n ‚â† ‚ä§ := by finiteness
  /- Consider a sequence of closed sets `s n` with `edist (s n) (s (n+1)) < B n`.
    We will show that it converges. The limit set is `t0 = ‚ãÇn, closure (‚ãÉm‚â•n, s m)`.
    We will have to show that a point in `s n` is close to a point in `t0`, and a point
    in `t0` is close to a point in `s n`. The completeness then follows from a
    standard criterion. -/
  refine EMetric.complete_of_convergent_controlled_sequences B B_pos fun s hs => ?_
  let t0 := ‚ãÇ n, closure (‚ãÉ m ‚â• n, s m : Set Œ±)
  let t : Closeds Œ± := ‚ü®t0, isClosed_iInter fun _ => isClosed_closure‚ü©
  use t
  -- The inequality is written this way to agree with `edist_le_of_edist_le_geometric_of_tendsto‚ÇÄ`
  have I1 : ‚àÄ n, ‚àÄ x ‚àà s n, ‚àÉ y ‚àà t0, edist x y ‚â§ 2 * B n := by
    /- This is the main difficulty of the proof. Starting from `x ‚àà s n`, we want
           to find a point in `t0` which is close to `x`. Define inductively a sequence of
           points `z m` with `z n = x` and `z m ‚àà s m` and `edist (z m) (z (m+1)) ‚â§ B m`. This is
           possible since the Hausdorff distance between `s m` and `s (m+1)` is at most `B m`.
           This sequence is a Cauchy sequence, therefore converging as the space is complete, to
           a limit which satisfies the required properties. -/
    intro n x hx
    obtain ‚ü®z, hz‚ÇÄ, hz‚ü© :
      ‚àÉ z : ‚àÄ l, s (n + l), (z 0 : Œ±) = x ‚àß ‚àÄ k, edist (z k : Œ±) (z (k + 1) : Œ±) ‚â§ B n / 2 ^ k := by
      -- We prove existence of the sequence by induction.
      have : ‚àÄ (l) (z : s (n + l)), ‚àÉ z' : s (n + l + 1), edist (z : Œ±) z' ‚â§ B n / 2 ^ l := by
        intro l z
        obtain ‚ü®z', z'_mem, hz'‚ü© : ‚àÉ z' ‚àà s (n + l + 1), edist (z : Œ±) z' < B n / 2 ^ l := by
          refine exists_edist_lt_of_hausdorffEDist_lt (s := s (n + l)) z.2 ?_
          simp only [ENNReal.inv_pow, div_eq_mul_inv]
          rw [‚Üê pow_add]
          apply hs <;> simp
        exact ‚ü®‚ü®z', z'_mem‚ü©, le_of_lt hz'‚ü©
      use fun k => Nat.recOn k ‚ü®x, hx‚ü© fun l z => (this l z).choose
      simp only [Nat.add_zero, Nat.rec_zero, true_and]
      exact fun k => (this k _).choose_spec
    -- it follows from the previous bound that `z` is a Cauchy sequence
    have : CauchySeq fun k => (z k : Œ±) := cauchySeq_of_edist_le_geometric_two (B n) (B_ne_top n) hz
    -- therefore, it converges
    rcases cauchySeq_tendsto_of_complete this with ‚ü®y, y_lim‚ü©
    use y
    -- the limit point `y` will be the desired point, in `t0` and close to our initial point `x`.
    -- First, we check it belongs to `t0`.
    have : y ‚àà t0 :=
      mem_iInter.2 fun k =>
        mem_closure_of_tendsto y_lim
          (by
            simp only [exists_prop, Set.mem_iUnion, Filter.eventually_atTop]
            exact ‚ü®k, fun m hm => ‚ü®n + m, by lia, (z m).2‚ü©‚ü©)
    use this
    -- Then, we check that `y` is close to `x = z n`. This follows from the fact that `y`
    -- is the limit of `z k`, and the distance between `z n` and `z k` has already been estimated.
    rw [‚Üê hz‚ÇÄ]
    exact edist_le_of_edist_le_geometric_two_of_tendsto‚ÇÄ (B n) hz y_lim
  have I2 : ‚àÄ n, ‚àÄ x ‚àà t0, ‚àÉ y ‚àà s n, edist x y ‚â§ 2 * B n := by
    /- For the (much easier) reverse inequality, we start from a point `x ‚àà t0` and we want
            to find a point `y ‚àà s n` which is close to `x`.
            `x` belongs to `t0`, the intersection of the closures. In particular, it is well
            approximated by a point `z` in `‚ãÉm‚â•n, s m`, say in `s m`. Since `s m` and
            `s n` are close, this point is itself well approximated by a point `y` in `s n`,
            as required. -/
    intro n x xt0
    have : x ‚àà closure (‚ãÉ m ‚â• n, s m : Set Œ±) := by apply mem_iInter.1 xt0 n
    obtain ‚ü®z : Œ±, hz, Dxz : edist x z < B n‚ü© := EMetric.mem_closure_iff.1 this (B n) (B_pos n)
    simp only [exists_prop, Set.mem_iUnion] at hz
    obtain ‚ü®m : ‚Ñï, m_ge_n : m ‚â• n, hm : z ‚àà (s m : Set Œ±)‚ü© := hz
    have : hausdorffEDist (s m : Set Œ±) (s n) < B n := hs n m n m_ge_n (le_refl n)
    obtain ‚ü®y : Œ±, hy : y ‚àà (s n : Set Œ±), Dzy : edist z y < B n‚ü© :=
      exists_edist_lt_of_hausdorffEDist_lt hm this
    exact
      ‚ü®y, hy,
        calc
          edist x y ‚â§ edist x z + edist z y := edist_triangle _ _ _
          _ ‚â§ B n + B n := by gcongr
          _ = 2 * B n := (two_mul _).symm
          ‚ü©
  -- Deduce from the above inequalities that the distance between `s n` and `t0` is at most `2 B n`.
  have main : ‚àÄ n : ‚Ñï, edist (s n) t ‚â§ 2 * B n := fun n =>
    hausdorffEDist_le_of_mem_edist (I1 n) (I2 n)
  -- from this, the convergence of `s n` to `t0` follows.
  refine EMetric.tendsto_atTop.2 fun Œµ Œµpos => ?_
  have : Tendsto (fun n => 2 * B n) atTop (ùìù (2 * 0)) :=
    ENNReal.Tendsto.const_mul (ENNReal.tendsto_pow_atTop_nhds_zero_of_lt_one <|
      by simp) (Or.inr <| by simp)
  rw [mul_zero] at this
  obtain ‚ü®N, hN‚ü© : ‚àÉ N, ‚àÄ b ‚â• N, Œµ > 2 * B b :=
    ((tendsto_order.1 this).2 Œµ Œµpos).exists_forall_of_atTop
  exact ‚ü®N, fun n hn => lt_of_le_of_lt (main n) (hN n hn)‚ü©

/-- In a compact space, the type of closed subsets is compact. -/
instance instCompactSpace [CompactSpace Œ±] : CompactSpace (Closeds Œ±) :=
  ‚ü®by
    have := Closeds.totallyBounded_subsets_of_totallyBounded (Œ± := Œ±) isCompact_univ.totallyBounded
    simp_rw [subset_univ, setOf_true] at this
    exact this.isCompact_of_isClosed isClosed_univ‚ü©

theorem isometry_singleton : Isometry ({¬∑} : Œ± ‚Üí Closeds Œ±) :=
  fun _ _ => hausdorffEDist_singleton

theorem lipschitz_sup : LipschitzWith 1 fun p : Closeds Œ± √ó Closeds Œ± => p.1 ‚äî p.2 :=
  .of_edist_le fun _ _ => hausdorffEDist_union_le

end Closeds

namespace NonemptyCompacts

/-- In an emetric space, the type of non-empty compact subsets is an emetric space,
where the edistance is the Hausdorff edistance -/
instance instEMetricSpace : EMetricSpace (NonemptyCompacts Œ±) where
  /- Since the topology on `NonemptyCompacts` is not defeq to the one induced by
  `UniformSpace.hausdorff`, we replace the uniformity by `NonemptyCompacts.uniformSpace`, which has
  the right topology. -/
  __ := (PseudoEMetricSpace.hausdorff.induced SetLike.coe).replaceUniformity <| by rfl
  eq_of_edist_eq_zero {s t} h := NonemptyCompacts.ext <| by
    have : closure (s : Set Œ±) = closure t := hausdorffEDist_zero_iff_closure_eq_closure.1 h
    rwa [s.isCompact.isClosed.closure_eq, t.isCompact.isClosed.closure_eq] at this

/-- `NonemptyCompacts.toCloseds` is an isometry -/
theorem isometry_toCloseds : Isometry (@NonemptyCompacts.toCloseds Œ± _ _) :=
  fun _ _ => rfl

/-- The range of `NonemptyCompacts.toCloseds` is closed in a complete space -/
theorem isClosed_in_closeds [CompleteSpace Œ±] :
    IsClosed (range <| @NonemptyCompacts.toCloseds Œ± _ _) := by
  have :
    range NonemptyCompacts.toCloseds =
      { s : Closeds Œ± | (s : Set Œ±).Nonempty ‚àß IsCompact (s : Set Œ±) } := by
    ext s
    refine ‚ü®?_, fun h => ‚ü®‚ü®‚ü®s, h.2‚ü©, h.1‚ü©, Closeds.ext rfl‚ü©‚ü©
    rintro ‚ü®s, hs, rfl‚ü©
    exact ‚ü®s.nonempty, s.isCompact‚ü©
  rw [this]
  refine isClosed_of_closure_subset fun s hs => ‚ü®?_, ?_‚ü©
  ¬∑ -- take a set t which is nonempty and at a finite distance of s
    rcases EMetric.mem_closure_iff.1 hs ‚ä§ ENNReal.coe_lt_top with ‚ü®t, ht, Dst‚ü©
    rw [edist_comm] at Dst
    -- since `t` is nonempty, so is `s`
    exact nonempty_of_hausdorffEDist_ne_top ht.1 (ne_of_lt Dst)
  ¬∑ refine isCompact_iff_totallyBounded_isComplete.2 ‚ü®?_, s.isClosed.isComplete‚ü©
    refine EMetric.totallyBounded_iff.2 fun Œµ (Œµpos : 0 < Œµ) => ?_
    -- we have to show that s is covered by finitely many eballs of radius Œµ
    -- pick a nonempty compact set t at distance at most Œµ/2 of s
    rcases EMetric.mem_closure_iff.1 hs (Œµ / 2) (ENNReal.half_pos Œµpos.ne') with ‚ü®t, ht, Dst‚ü©
    -- cover this space with finitely many balls of radius Œµ/2
    rcases EMetric.totallyBounded_iff.1 (isCompact_iff_totallyBounded_isComplete.1 ht.2).1 (Œµ / 2)
        (ENNReal.half_pos Œµpos.ne') with ‚ü®u, fu, ut‚ü©
    refine ‚ü®u, ‚ü®fu, fun x hx => ?_‚ü©‚ü©
    -- u : set Œ±, fu : u.finite, ut : t ‚äÜ ‚ãÉ (y : Œ±) (H : y ‚àà u), eball y (Œµ / 2)
    -- then s is covered by the union of the balls centered at u of radius Œµ
    rcases exists_edist_lt_of_hausdorffEDist_lt hx Dst with ‚ü®z, hz, Dxz‚ü©
    rcases mem_iUnion‚ÇÇ.1 (ut hz) with ‚ü®y, hy, Dzy‚ü©
    have : edist x y < Œµ :=
      calc
        edist x y ‚â§ edist x z + edist z y := edist_triangle _ _ _
        _ < Œµ / 2 + Œµ / 2 := ENNReal.add_lt_add Dxz Dzy
        _ = Œµ := ENNReal.add_halves _
    exact mem_biUnion hy this

/-- In a complete space, the type of nonempty compact subsets is complete. This follows
from the same statement for closed subsets -/
instance instCompleteSpace [CompleteSpace Œ±] : CompleteSpace (NonemptyCompacts Œ±) :=
  (completeSpace_iff_isComplete_range
        isometry_toCloseds.isUniformInducing).2 <|
    isClosed_in_closeds.isComplete

/-- In a compact space, the type of nonempty compact subsets is compact. This follows from
the same statement for closed subsets -/
instance instCompactSpace [CompactSpace Œ±] : CompactSpace (NonemptyCompacts Œ±) :=
  ‚ü®by
    rw [isometry_toCloseds.isEmbedding.isCompact_iff, image_univ]
    exact isClosed_in_closeds.isCompact‚ü©

/-- In a second countable space, the type of nonempty compact subsets is second countable -/
instance instSecondCountableTopology [SecondCountableTopology Œ±] :
    SecondCountableTopology (NonemptyCompacts Œ±) :=
  haveI : SeparableSpace (NonemptyCompacts Œ±) := by
    /- To obtain a countable dense subset of `NonemptyCompacts Œ±`, start from
        a countable dense subset `s` of Œ±, and then consider all its finite nonempty subsets.
        This set is countable and made of nonempty compact sets. It turns out to be dense:
        by total boundedness, any compact set `t` can be covered by finitely many small balls, and
        approximations in `s` of the centers of these balls give the required finite approximation
        of `t`. -/
    rcases exists_countable_dense Œ± with ‚ü®s, cs, s_dense‚ü©
    let v0 := { t : Set Œ± | t.Finite ‚àß t ‚äÜ s }
    let v : Set (NonemptyCompacts Œ±) := { t : NonemptyCompacts Œ± | (t : Set Œ±) ‚àà v0 }
    refine ‚ü®‚ü®v, ?_, ?_‚ü©‚ü©
    ¬∑ have : v0.Countable := countable_setOf_finite_subset cs
      exact this.preimage SetLike.coe_injective
    ¬∑ refine fun t => EMetric.mem_closure_iff.2 fun Œµ Œµpos => ?_
      -- t is a compact nonempty set, that we have to approximate uniformly by a a set in `v`.
      rcases exists_between Œµpos with ‚ü®Œ¥, Œ¥pos, Œ¥lt‚ü©
      have Œ¥pos' : 0 < Œ¥ / 2 := ENNReal.half_pos Œ¥pos.ne'
      -- construct a map F associating to a point in Œ± an approximating point in s, up to Œ¥/2.
      have Exy : ‚àÄ x, ‚àÉ y, y ‚àà s ‚àß edist x y < Œ¥ / 2 := by
        intro x
        rcases EMetric.mem_closure_iff.1 (s_dense x) (Œ¥ / 2) Œ¥pos' with ‚ü®y, ys, hy‚ü©
        exact ‚ü®y, ‚ü®ys, hy‚ü©‚ü©
      let F x := (Exy x).choose
      have Fspec : ‚àÄ x, F x ‚àà s ‚àß edist x (F x) < Œ¥ / 2 := fun x => (Exy x).choose_spec
      -- cover `t` with finitely many balls. Their centers form a set `a`
      have : TotallyBounded (t : Set Œ±) := t.isCompact.totallyBounded
      obtain ‚ü®a : Set Œ±, af : Set.Finite a, ta : (t : Set Œ±) ‚äÜ ‚ãÉ y ‚àà a, EMetric.ball y (Œ¥ / 2)‚ü© :=
        EMetric.totallyBounded_iff.1 this (Œ¥ / 2) Œ¥pos'
      -- replace each center by a nearby approximation in `s`, giving a new set `b`
      let b := F '' a
      have : b.Finite := af.image _
      have tb : ‚àÄ x ‚àà t, ‚àÉ y ‚àà b, edist x y < Œ¥ := by
        intro x hx
        rcases mem_iUnion‚ÇÇ.1 (ta hx) with ‚ü®z, za, Dxz‚ü©
        exists F z, mem_image_of_mem _ za
        calc
          edist x (F z) ‚â§ edist x z + edist z (F z) := edist_triangle _ _ _
          _ < Œ¥ / 2 + Œ¥ / 2 := ENNReal.add_lt_add Dxz (Fspec z).2
          _ = Œ¥ := ENNReal.add_halves _
      -- keep only the points in `b` that are close to point in `t`, yielding a new set `c`
      let c := { y ‚àà b | ‚àÉ x ‚àà t, edist x y < Œ¥ }
      have : c.Finite := ‚Äπb.Finite‚Ä∫.subset fun x hx => hx.1
      -- points in `t` are well approximated by points in `c`
      have tc : ‚àÄ x ‚àà t, ‚àÉ y ‚àà c, edist x y ‚â§ Œ¥ := by
        intro x hx
        rcases tb x hx with ‚ü®y, yv, Dxy‚ü©
        have : y ‚àà c := by simpa [c, -mem_image] using ‚ü®yv, ‚ü®x, hx, Dxy‚ü©‚ü©
        exact ‚ü®y, this, le_of_lt Dxy‚ü©
      -- points in `c` are well approximated by points in `t`
      have ct : ‚àÄ y ‚àà c, ‚àÉ x ‚àà t, edist y x ‚â§ Œ¥ := by
        rintro y ‚ü®_, x, xt, Dyx‚ü©
        have : edist y x ‚â§ Œ¥ :=
          calc
            edist y x = edist x y := edist_comm _ _
            _ ‚â§ Œ¥ := le_of_lt Dyx
        exact ‚ü®x, xt, this‚ü©
      -- it follows that their Hausdorff distance is small
      have : hausdorffEDist (t : Set Œ±) c ‚â§ Œ¥ := hausdorffEDist_le_of_mem_edist tc ct
      have Dtc : hausdorffEDist (t : Set Œ±) c < Œµ := this.trans_lt Œ¥lt
      -- the set `c` is not empty, as it is well approximated by a nonempty set
      have hc : c.Nonempty := nonempty_of_hausdorffEDist_ne_top t.nonempty (ne_top_of_lt Dtc)
      -- let `d` be the version of `c` in the type `NonemptyCompacts Œ±`
      let d : NonemptyCompacts Œ± := ‚ü®‚ü®c, ‚Äπc.Finite‚Ä∫.isCompact‚ü©, hc‚ü©
      have : c ‚äÜ s := by
        intro x hx
        rcases (mem_image _ _ _).1 hx.1 with ‚ü®y, ‚ü®_, yx‚ü©‚ü©
        rw [‚Üê yx]
        exact (Fspec y).1
      have : d ‚àà v := ‚ü®‚Äπc.Finite‚Ä∫, this‚ü©
      -- we have proved that `d` is a good approximation of `t` as requested
      exact ‚ü®d, ‚Äπd ‚àà v‚Ä∫, Dtc‚ü©
  UniformSpace.secondCountable_of_separable (NonemptyCompacts Œ±)

theorem isometry_singleton : Isometry ({¬∑} : Œ± ‚Üí NonemptyCompacts Œ±) :=
  fun _ _ => hausdorffEDist_singleton

theorem lipschitz_sup :
    LipschitzWith 1 fun p : NonemptyCompacts Œ± √ó NonemptyCompacts Œ± => p.1 ‚äî p.2 :=
  .of_edist_le fun _ _ => hausdorffEDist_union_le

theorem lipschitz_prod :
    LipschitzWith 1 fun p : NonemptyCompacts Œ± √ó NonemptyCompacts Œ≤ => p.1 √óÀ¢ p.2 :=
  .of_edist_le fun _ _ => hausdorffEDist_prod_le

end NonemptyCompacts

end TopologicalSpace

namespace EMetric

open Metric

@[deprecated (since := "2025-11-19")]
alias NonemptyCompacts.continuous_toCloseds :=
  TopologicalSpace.NonemptyCompacts.continuous_toCloseds

@[deprecated (since := "2025-08-20")]
alias isClosed_subsets_of_isClosed := TopologicalSpace.Closeds.isClosed_subsets_of_isClosed

@[deprecated (since := "2025-11-19")]
alias NonemptyCompacts.isClosed_subsets_of_isClosed :=
  TopologicalSpace.NonemptyCompacts.isClosed_subsets_of_isClosed

@[deprecated (since := "2025-11-19")]
alias Closeds.isClosed_subsets_of_isClosed :=
  TopologicalSpace.Closeds.isClosed_subsets_of_isClosed

@[deprecated (since := "2026-01-08")]
alias mem_hausdorffEntourage_of_hausdorffEdist_lt :=
  mem_hausdorffEntourage_of_hausdorffEDist_lt

@[deprecated (since := "2026-01-08")]
alias hausdorffEdist_le_of_mem_hausdorffEntourage := hausdorffEDist_le_of_mem_hausdorffEntourage

@[deprecated (since := "2026-01-08")]
alias continuous_infEdist_hausdorffEdist :=
  TopologicalSpace.Closeds.continuous_infEDist

@[deprecated (since := "2026-01-08")]
alias Closeds.edist_eq := TopologicalSpace.Closeds.edist_eq

@[deprecated (since := "2026-01-08")]
alias Closeds.isometry_singleton := TopologicalSpace.Closeds.isometry_singleton

@[deprecated (since := "2026-01-08")]
alias Closeds.lipschitz_sup := TopologicalSpace.Closeds.lipschitz_sup

@[deprecated (since := "2026-01-08")]
alias NonemptyCompacts.isometry_toCloseds :=
  TopologicalSpace.NonemptyCompacts.isometry_toCloseds

@[deprecated (since := "2025-08-20")]
alias NonemptyCompacts.ToCloseds.isUniformEmbedding :=
  TopologicalSpace.NonemptyCompacts.isUniformEmbedding_toCloseds

@[deprecated (since := "2025-11-19")]
alias NonemptyCompacts.isUniformEmbedding_toCloseds :=
  TopologicalSpace.NonemptyCompacts.isUniformEmbedding_toCloseds

@[deprecated (since := "2026-01-08")]
alias NonemptyCompacts.isClosed_in_closeds :=
  TopologicalSpace.NonemptyCompacts.isClosed_in_closeds

@[deprecated (since := "2026-01-08")]
alias NonemptyCompacts.isometry_singleton :=
  TopologicalSpace.NonemptyCompacts.isometry_singleton

@[deprecated (since := "2026-01-08")]
alias NonemptyCompacts.lipschitz_sup :=
  TopologicalSpace.NonemptyCompacts.lipschitz_sup

@[deprecated (since := "2026-01-08")]
alias NonemptyCompacts.lipschitz_prod :=
  TopologicalSpace.NonemptyCompacts.lipschitz_prod

end EMetric --namespace

namespace Metric

section

variable {Œ± : Type*} [MetricSpace Œ±]

/-- `NonemptyCompacts Œ±` inherits a metric space structure, as the Hausdorff
edistance between two such sets is finite. -/
instance NonemptyCompacts.instMetricSpace : MetricSpace (NonemptyCompacts Œ±) :=
  EMetricSpace.toMetricSpace fun x y =>
    hausdorffEDist_ne_top_of_nonempty_of_bounded x.nonempty y.nonempty x.isCompact.isBounded
      y.isCompact.isBounded

/-- The distance on `NonemptyCompacts Œ±` is the Hausdorff distance, by construction -/
theorem NonemptyCompacts.dist_eq {x y : NonemptyCompacts Œ±} :
    dist x y = hausdorffDist (x : Set Œ±) y :=
  rfl

theorem lipschitz_infDist_set (x : Œ±) : LipschitzWith 1 fun s : NonemptyCompacts Œ± => infDist x s :=
  LipschitzWith.of_le_add fun s t => by
    rw [dist_comm]
    exact infDist_le_infDist_add_hausdorffDist (edist_ne_top t s)

theorem lipschitz_infDist : LipschitzWith 2 fun p : Œ± √ó NonemptyCompacts Œ± => infDist p.1 p.2 := by
  rw [‚Üê one_add_one_eq_two]
  exact LipschitzWith.uncurry
    (fun s : NonemptyCompacts Œ± => lipschitz_infDist_pt (s : Set Œ±)) lipschitz_infDist_set

theorem uniformContinuous_infDist_Hausdorff_dist :
    UniformContinuous fun p : Œ± √ó NonemptyCompacts Œ± => infDist p.1 p.2 :=
  lipschitz_infDist.uniformContinuous

end --section

end Metric --namespace
