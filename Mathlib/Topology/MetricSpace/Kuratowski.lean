/-
Copyright (c) 2018 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.NormedSpace.lpSpace
import Mathlib.Topology.Sets.Compacts

#align_import topology.metric_space.kuratowski from "leanprover-community/mathlib"@"95d4f6586d313c8c28e00f36621d2a6a66893aa6"

/-!
# The Kuratowski embedding

Any separable metric space can be embedded isometrically in `â„“^âˆž(â„•, â„)`.
Any partially defined Lipschitz map into `â„“^âˆž` can be extended to the whole space.

-/

set_option autoImplicit true


noncomputable section

set_option linter.uppercaseLean3 false

open Set Metric TopologicalSpace NNReal ENNReal lp Function

universe u v w

variable {Î± : Type u} {Î² : Type v} {Î³ : Type w}

namespace KuratowskiEmbedding

/-! ### Any separable metric space can be embedded isometrically in â„“^âˆž(â„•, â„) -/


variable {f g : â„“^âˆž(â„•)} {n : â„•} {C : â„} [MetricSpace Î±] (x : â„• â†’ Î±) (a b : Î±)

/-- A metric space can be embedded in `l^âˆž(â„)` via the distances to points in
a fixed countable set, if this set is dense. This map is given in `kuratowskiEmbedding`,
without density assumptions. -/
def embeddingOfSubset : â„“^âˆž(â„•) :=
  âŸ¨fun n => dist a (x n) - dist (x 0) (x n), by
    apply memâ„“p_infty
    -- âŠ¢ BddAbove (range fun i => â€–(fun n => dist a (x n) - dist (x 0) (x n)) iâ€–)
    use dist a (x 0)
    -- âŠ¢ dist a (x 0) âˆˆ upperBounds (range fun i => â€–(fun n => dist a (x n) - dist (x â€¦
    rintro - âŸ¨n, rflâŸ©
    -- âŠ¢ (fun i => â€–(fun n => dist a (x n) - dist (x 0) (x n)) iâ€–) n â‰¤ dist a (x 0)
    exact abs_dist_sub_le _ _ _âŸ©
    -- ðŸŽ‰ no goals
#align Kuratowski_embedding.embedding_of_subset KuratowskiEmbedding.embeddingOfSubset

theorem embeddingOfSubset_coe : embeddingOfSubset x a n = dist a (x n) - dist (x 0) (x n) :=
  rfl
#align Kuratowski_embedding.embedding_of_subset_coe KuratowskiEmbedding.embeddingOfSubset_coe

/-- The embedding map is always a semi-contraction. -/
theorem embeddingOfSubset_dist_le (a b : Î±) :
    dist (embeddingOfSubset x a) (embeddingOfSubset x b) â‰¤ dist a b := by
  refine' lp.norm_le_of_forall_le dist_nonneg fun n => _
  -- âŠ¢ â€–â†‘(embeddingOfSubset x a - embeddingOfSubset x b) nâ€– â‰¤ dist a b
  simp only [lp.coeFn_sub, Pi.sub_apply, embeddingOfSubset_coe, Real.dist_eq]
  -- âŠ¢ â€–dist a (x n) - dist (x 0) (x n) - (dist b (x n) - dist (x 0) (x n))â€– â‰¤ dist â€¦
  convert abs_dist_sub_le a b (x n) using 2
  -- âŠ¢ dist a (x n) - dist (x 0) (x n) - (dist b (x n) - dist (x 0) (x n)) = dist a â€¦
  ring
  -- ðŸŽ‰ no goals
#align Kuratowski_embedding.embedding_of_subset_dist_le KuratowskiEmbedding.embeddingOfSubset_dist_le

/-- When the reference set is dense, the embedding map is an isometry on its image. -/
theorem embeddingOfSubset_isometry (H : DenseRange x) : Isometry (embeddingOfSubset x) := by
  refine' Isometry.of_dist_eq fun a b => _
  -- âŠ¢ dist (embeddingOfSubset x a) (embeddingOfSubset x b) = dist a b
  refine' (embeddingOfSubset_dist_le x a b).antisymm (le_of_forall_pos_le_add fun e epos => _)
  -- âŠ¢ dist a b â‰¤ dist (embeddingOfSubset x a) (embeddingOfSubset x b) + e
  -- First step: find n with dist a (x n) < e
  rcases Metric.mem_closure_range_iff.1 (H a) (e / 2) (half_pos epos) with âŸ¨n, hnâŸ©
  -- âŠ¢ dist a b â‰¤ dist (embeddingOfSubset x a) (embeddingOfSubset x b) + e
  -- Second step: use the norm control at index n to conclude
  have C : dist b (x n) - dist a (x n) = embeddingOfSubset x b n - embeddingOfSubset x a n := by
    simp only [embeddingOfSubset_coe, sub_sub_sub_cancel_right]
  have :=
    calc
      dist a b â‰¤ dist a (x n) + dist (x n) b := dist_triangle _ _ _
      _ = 2 * dist a (x n) + (dist b (x n) - dist a (x n)) := by simp [dist_comm]; ring
      _ â‰¤ 2 * dist a (x n) + |dist b (x n) - dist a (x n)| := by
        apply_rules [add_le_add_left, le_abs_self]
      _ â‰¤ 2 * (e / 2) + |embeddingOfSubset x b n - embeddingOfSubset x a n| := by
        rw [C]
        apply_rules [add_le_add, mul_le_mul_of_nonneg_left, hn.le, le_refl]
        norm_num
      _ â‰¤ 2 * (e / 2) + dist (embeddingOfSubset x b) (embeddingOfSubset x a) := by
        have : |embeddingOfSubset x b n - embeddingOfSubset x a n| â‰¤
            dist (embeddingOfSubset x b) (embeddingOfSubset x a) := by
          simp only [dist_eq_norm]
          exact lp.norm_apply_le_norm ENNReal.top_ne_zero
            (embeddingOfSubset x b - embeddingOfSubset x a) n
        nlinarith
      _ = dist (embeddingOfSubset x b) (embeddingOfSubset x a) + e := by ring
  simpa [dist_comm] using this
  -- ðŸŽ‰ no goals
#align Kuratowski_embedding.embedding_of_subset_isometry KuratowskiEmbedding.embeddingOfSubset_isometry

/-- Every separable metric space embeds isometrically in `â„“^âˆž(â„•)`. -/
theorem exists_isometric_embedding (Î± : Type u) [MetricSpace Î±] [SeparableSpace Î±] :
    âˆƒ f : Î± â†’ â„“^âˆž(â„•), Isometry f := by
  cases' (univ : Set Î±).eq_empty_or_nonempty with h h
  -- âŠ¢ âˆƒ f, Isometry f
  Â· use fun _ => 0; intro x; exact absurd h (Nonempty.ne_empty âŸ¨x, mem_univ xâŸ©)
    -- âŠ¢ Isometry fun x => 0
                    -- âŠ¢ âˆ€ (x2 : Î±), edist ((fun x => 0) x) ((fun x => 0) x2) = edist x x2
                             -- ðŸŽ‰ no goals
  Â· -- We construct a map x : â„• â†’ Î± with dense image
    rcases h with âŸ¨basepointâŸ©
    -- âŠ¢ âˆƒ f, Isometry f
    haveI : Inhabited Î± := âŸ¨basepointâŸ©
    -- âŠ¢ âˆƒ f, Isometry f
    have : âˆƒ s : Set Î±, s.Countable âˆ§ Dense s := exists_countable_dense Î±
    -- âŠ¢ âˆƒ f, Isometry f
    rcases this with âŸ¨S, âŸ¨S_countable, S_denseâŸ©âŸ©
    -- âŠ¢ âˆƒ f, Isometry f
    rcases Set.countable_iff_exists_subset_range.1 S_countable with âŸ¨x, x_rangeâŸ©
    -- âŠ¢ âˆƒ f, Isometry f
    -- Use embeddingOfSubset to construct the desired isometry
    exact âŸ¨embeddingOfSubset x, embeddingOfSubset_isometry x (S_dense.mono x_range)âŸ©
    -- ðŸŽ‰ no goals
#align Kuratowski_embedding.exists_isometric_embedding KuratowskiEmbedding.exists_isometric_embedding

end KuratowskiEmbedding

open TopologicalSpace KuratowskiEmbedding

/-- The Kuratowski embedding is an isometric embedding of a separable metric space in `â„“^âˆž(â„•, â„)`.
-/
def kuratowskiEmbedding (Î± : Type u) [MetricSpace Î±] [SeparableSpace Î±] : Î± â†’ â„“^âˆž(â„•) :=
  Classical.choose (KuratowskiEmbedding.exists_isometric_embedding Î±)
#align Kuratowski_embedding kuratowskiEmbedding

/--
The Kuratowski embedding is an isometry.
Theorem 2.1 of [Assaf Naor, *Metric Embeddings and Lipschitz Extensions*][Naor-2015]. -/
protected theorem kuratowskiEmbedding.isometry (Î± : Type u) [MetricSpace Î±] [SeparableSpace Î±] :
    Isometry (kuratowskiEmbedding Î±) :=
  Classical.choose_spec (exists_isometric_embedding Î±)
#align Kuratowski_embedding.isometry kuratowskiEmbedding.isometry

/-- Version of the Kuratowski embedding for nonempty compacts -/
nonrec def NonemptyCompacts.kuratowskiEmbedding (Î± : Type u) [MetricSpace Î±] [CompactSpace Î±]
    [Nonempty Î±] : NonemptyCompacts â„“^âˆž(â„•) where
  carrier := range (kuratowskiEmbedding Î±)
  isCompact' := isCompact_range (kuratowskiEmbedding.isometry Î±).continuous
  nonempty' := range_nonempty _
#align nonempty_compacts.Kuratowski_embedding NonemptyCompacts.kuratowskiEmbedding

/--
A function `f : Î± â†’ â„“^âˆž(Î¹, â„)` which is `K`-Lipschitz on a subset `s` admits a `K`-Lipschitz
extension to the whole space.

Theorem 2.2 of [Assaf Naor, *Metric Embeddings and Lipschitz Extensions*][Naor-2015]

The same result for the case of a finite type `Î¹` is implemented in
`LipschitzOnWith.extend_pi`.
-/
theorem LipschitzOnWith.extend_lp_infty [PseudoMetricSpace Î±] {s : Set Î±} {f : Î± â†’ â„“^âˆž(Î¹)}
    {K : â„â‰¥0} (hfl : LipschitzOnWith K f s): âˆƒ g : Î± â†’ â„“^âˆž(Î¹), LipschitzWith K g âˆ§ EqOn f g s := by
  -- Construct the coordinate-wise extensions
  rw [LipschitzOnWith.coordinate] at hfl
  -- âŠ¢ âˆƒ g, LipschitzWith K g âˆ§ EqOn f g s
  have : âˆ€ i : Î¹, âˆƒ g : Î± â†’ â„, LipschitzWith K g âˆ§ EqOn (fun x => f x i) g s
  -- âŠ¢ âˆ€ (i : Î¹), âˆƒ g, LipschitzWith K g âˆ§ EqOn (fun x => â†‘(f x) i) g s
  Â· intro i
    -- âŠ¢ âˆƒ g, LipschitzWith K g âˆ§ EqOn (fun x => â†‘(f x) i) g s
    exact LipschitzOnWith.extend_real (hfl i) -- use the nonlinear Hahn-Banach theorem here!
    -- ðŸŽ‰ no goals
  choose g hgl hgeq using this
  -- âŠ¢ âˆƒ g, LipschitzWith K g âˆ§ EqOn f g s
  rcases s.eq_empty_or_nonempty with rfl | âŸ¨aâ‚€, haâ‚€_in_sâŸ©
  -- âŠ¢ âˆƒ g, LipschitzWith K g âˆ§ EqOn f g âˆ…
  Â· exact âŸ¨0, LipschitzWith.const' 0, by simpâŸ©
    -- ðŸŽ‰ no goals
  Â· -- Show that the extensions are uniformly bounded
    have hf_extb : âˆ€ a : Î±, Memâ„“p (swap g a) âˆž
    -- âŠ¢ âˆ€ (a : Î±), Memâ„“p (swap g a) âŠ¤
    Â· apply LipschitzWith.uniformly_bounded (swap g) hgl aâ‚€
      -- âŠ¢ Memâ„“p (swap g aâ‚€) âŠ¤
      use â€–f aâ‚€â€–
      -- âŠ¢ â€–f aâ‚€â€– âˆˆ upperBounds (range fun i => â€–swap g aâ‚€ iâ€–)
      rintro - âŸ¨i, rflâŸ©
      -- âŠ¢ (fun i => â€–swap g aâ‚€ iâ€–) i â‰¤ â€–f aâ‚€â€–
      simp_rw [â†hgeq i haâ‚€_in_s]
      -- âŠ¢ â€–â†‘(f aâ‚€) iâ€– â‰¤ â€–f aâ‚€â€–
      exact lp.norm_apply_le_norm top_ne_zero (f aâ‚€) i
      -- ðŸŽ‰ no goals
    -- Construct witness by bundling the function with its certificate of membership in â„“^âˆž
    let f_ext' : Î± â†’ â„“^âˆž(Î¹) := fun i â†¦ âŸ¨swap g i, hf_extb iâŸ©
    -- âŠ¢ âˆƒ g, LipschitzWith K g âˆ§ EqOn f g s
    refine âŸ¨f_ext', ?_, ?_âŸ©
    -- âŠ¢ LipschitzWith K f_ext'
    Â· rw [LipschitzWith.coordinate]
      -- âŠ¢ âˆ€ (i : Î¹), LipschitzWith K fun a => â†‘(f_ext' a) i
      exact hgl
      -- ðŸŽ‰ no goals
    Â· intro a hyp
      -- âŠ¢ f a = f_ext' a
      ext i
      -- âŠ¢ â†‘(f a) i = â†‘(f_ext' a) i
      exact (hgeq i) hyp
      -- ðŸŽ‰ no goals
