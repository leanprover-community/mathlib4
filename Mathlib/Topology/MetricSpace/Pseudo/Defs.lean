/-
Copyright (c) 2015, 2017 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Robert Y. Lewis, Johannes H√∂lzl, Mario Carneiro, S√©bastien Gou√´zel
-/
import Mathlib.Data.ENNReal.Real
import Mathlib.Tactic.Bound.Attribute
import Mathlib.Topology.Bornology.Basic
import Mathlib.Topology.EMetricSpace.Defs
import Mathlib.Topology.UniformSpace.Basic

/-!
## Pseudo-metric spaces

This file defines pseudo-metric spaces: these differ from metric spaces by not imposing the
condition `dist x y = 0 ‚Üí x = y`.
Many definitions and theorems expected on (pseudo-)metric spaces are already introduced on uniform
spaces and topological spaces. For example: open and closed sets, compactness, completeness,
continuity and uniform continuity.

## Main definitions

* `Dist Œ±`: Endows a space `Œ±` with a function `dist a b`.
* `PseudoMetricSpace Œ±`: A space endowed with a distance function, which can
  be zero even if the two elements are non-equal.
* `Metric.ball x Œµ`: The set of all points `y` with `dist y x < Œµ`.
* `Metric.Bounded s`: Whether a subset of a `PseudoMetricSpace` is bounded.
* `MetricSpace Œ±`: A `PseudoMetricSpace` with the guarantee `dist x y = 0 ‚Üí x = y`.

Additional useful definitions:

* `nndist a b`: `dist` as a function to the non-negative reals.
* `Metric.closedBall x Œµ`: The set of all points `y` with `dist y x ‚â§ Œµ`.
* `Metric.sphere x Œµ`: The set of all points `y` with `dist y x = Œµ`.

TODO (anyone): Add "Main results" section.

## Tags

pseudo_metric, dist
-/

assert_not_exists compactSpace_uniformity

open Set Filter TopologicalSpace Bornology
open scoped ENNReal NNReal Uniformity Topology

universe u v w

variable {Œ± : Type u} {Œ≤ : Type v} {X Œπ : Type*}

theorem UniformSpace.ofDist_aux (Œµ : ‚Ñù) (hŒµ : 0 < Œµ) : ‚àÉ Œ¥ > (0 : ‚Ñù), ‚àÄ x < Œ¥, ‚àÄ y < Œ¥, x + y < Œµ :=
  ‚ü®Œµ / 2, half_pos hŒµ, fun _x hx _y hy => add_halves Œµ ‚ñ∏ add_lt_add hx hy‚ü©

/-- Construct a uniform structure from a distance function and metric space axioms -/
def UniformSpace.ofDist (dist : Œ± ‚Üí Œ± ‚Üí ‚Ñù) (dist_self : ‚àÄ x : Œ±, dist x x = 0)
    (dist_comm : ‚àÄ x y : Œ±, dist x y = dist y x)
    (dist_triangle : ‚àÄ x y z : Œ±, dist x z ‚â§ dist x y + dist y z) : UniformSpace Œ± :=
  .ofFun dist dist_self dist_comm dist_triangle ofDist_aux

/-- Construct a bornology from a distance function and metric space axioms. -/
abbrev Bornology.ofDist {Œ± : Type*} (dist : Œ± ‚Üí Œ± ‚Üí ‚Ñù) (dist_comm : ‚àÄ x y, dist x y = dist y x)
    (dist_triangle : ‚àÄ x y z, dist x z ‚â§ dist x y + dist y z) : Bornology Œ± :=
  Bornology.ofBounded { s : Set Œ± | ‚àÉ C, ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí ‚àÄ ‚¶Éy‚¶Ñ, y ‚àà s ‚Üí dist x y ‚â§ C }
    ‚ü®0, fun _ hx _ => hx.elim‚ü© (fun _ ‚ü®c, hc‚ü© _ h => ‚ü®c, fun _ hx _ hy => hc (h hx) (h hy)‚ü©)
    (fun s hs t ht => by
      rcases s.eq_empty_or_nonempty with rfl | ‚ü®x, hx‚ü©
      ¬∑ rwa [empty_union]
      rcases t.eq_empty_or_nonempty with rfl | ‚ü®y, hy‚ü©
      ¬∑ rwa [union_empty]
      rsuffices ‚ü®C, hC‚ü© : ‚àÉ C, ‚àÄ z ‚àà s ‚à™ t, dist x z ‚â§ C
      ¬∑ refine ‚ü®C + C, fun a ha b hb => (dist_triangle a x b).trans ?_‚ü©
        simpa only [dist_comm] using add_le_add (hC _ ha) (hC _ hb)
      rcases hs with ‚ü®Cs, hs‚ü©; rcases ht with ‚ü®Ct, ht‚ü©
      refine ‚ü®max Cs (dist x y + Ct), fun z hz => hz.elim
        (fun hz => (hs hx hz).trans (le_max_left _ _))
        (fun hz => (dist_triangle x y z).trans <|
          (add_le_add le_rfl (ht hy hz)).trans (le_max_right _ _))‚ü©)
    fun z => ‚ü®dist z z, forall_eq.2 <| forall_eq.2 le_rfl‚ü©

/-- The distance function (given an ambient metric space on `Œ±`), which returns
  a nonnegative real number `dist x y` given `x y : Œ±`. -/
@[ext]
class Dist (Œ± : Type*) where
  /-- Distance between two points -/
  dist : Œ± ‚Üí Œ± ‚Üí ‚Ñù

export Dist (dist)

-- the uniform structure and the emetric space structure are embedded in the metric space structure
-- to avoid instance diamond issues. See Note [forgetful inheritance].
/-- This is an internal lemma used inside the default of `PseudoMetricSpace.edist`. -/
private theorem dist_nonneg' {Œ±} {x y : Œ±} (dist : Œ± ‚Üí Œ± ‚Üí ‚Ñù)
    (dist_self : ‚àÄ x : Œ±, dist x x = 0) (dist_comm : ‚àÄ x y : Œ±, dist x y = dist y x)
    (dist_triangle : ‚àÄ x y z : Œ±, dist x z ‚â§ dist x y + dist y z) : 0 ‚â§ dist x y :=
  have : 0 ‚â§ 2 * dist x y :=
    calc 0 = dist x x := (dist_self _).symm
    _ ‚â§ dist x y + dist y x := dist_triangle _ _ _
    _ = 2 * dist x y := by rw [two_mul, dist_comm]
  nonneg_of_mul_nonneg_right this two_pos

/-- A pseudometric space is a type endowed with a `‚Ñù`-valued distance `dist` satisfying
reflexivity `dist x x = 0`, commutativity `dist x y = dist y x`, and the triangle inequality
`dist x z ‚â§ dist x y + dist y z`.

Note that we do not require `dist x y = 0 ‚Üí x = y`. See metric spaces (`MetricSpace`) for the
similar class with that stronger assumption.

Any pseudometric space is a topological space and a uniform space (see `TopologicalSpace`,
`UniformSpace`), where the topology and uniformity come from the metric.
Note that a T1 pseudometric space is just a metric space.

We make the uniformity/topology part of the data instead of deriving it from the metric. This e.g.
ensures that we do not get a diamond when doing
`[PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] : TopologicalSpace (Œ± √ó Œ≤)`:
The product metric and product topology agree, but not definitionally so.
See Note [forgetful inheritance]. -/
class PseudoMetricSpace (Œ± : Type u) : Type u extends Dist Œ± where
  dist_self : ‚àÄ x : Œ±, dist x x = 0
  dist_comm : ‚àÄ x y : Œ±, dist x y = dist y x
  dist_triangle : ‚àÄ x y z : Œ±, dist x z ‚â§ dist x y + dist y z
  /-- Extended distance between two points -/
  edist : Œ± ‚Üí Œ± ‚Üí ‚Ñù‚â•0‚àû := fun x y => ENNReal.ofNNReal ‚ü®dist x y, dist_nonneg' _ ‚Äπ_‚Ä∫ ‚Äπ_‚Ä∫ ‚Äπ_‚Ä∫‚ü©
  edist_dist : ‚àÄ x y : Œ±, edist x y = ENNReal.ofReal (dist x y) := by
    intro x y; exact ENNReal.coe_nnreal_eq _
  toUniformSpace : UniformSpace Œ± := .ofDist dist dist_self dist_comm dist_triangle
  uniformity_dist : ùì§ Œ± = ‚®Ö Œµ > 0, ùìü { p : Œ± √ó Œ± | dist p.1 p.2 < Œµ } := by intros; rfl
  toBornology : Bornology Œ± := Bornology.ofDist dist dist_comm dist_triangle
  cobounded_sets : (Bornology.cobounded Œ±).sets =
    { s | ‚àÉ C : ‚Ñù, ‚àÄ x ‚àà s·∂ú, ‚àÄ y ‚àà s·∂ú, dist x y ‚â§ C } := by intros; rfl

/-- Two pseudo metric space structures with the same distance function coincide. -/
@[ext]
theorem PseudoMetricSpace.ext {Œ± : Type*} {m m' : PseudoMetricSpace Œ±}
    (h : m.toDist = m'.toDist) : m = m' := by
  let d := m.toDist
  obtain ‚ü®_, _, _, _, hed, _, hU, _, hB‚ü© := m
  let d' := m'.toDist
  obtain ‚ü®_, _, _, _, hed', _, hU', _, hB'‚ü© := m'
  obtain rfl : d = d' := h
  congr
  ¬∑ ext x y : 2
    rw [hed, hed']
  ¬∑ exact UniformSpace.ext (hU.trans hU'.symm)
  ¬∑ ext : 2
    rw [‚Üê Filter.mem_sets, ‚Üê Filter.mem_sets, hB, hB']

variable [PseudoMetricSpace Œ±]

attribute [instance] PseudoMetricSpace.toUniformSpace PseudoMetricSpace.toBornology

-- see Note [lower instance priority]
instance (priority := 200) PseudoMetricSpace.toEDist : EDist Œ± :=
  ‚ü®PseudoMetricSpace.edist‚ü©

/-- Construct a pseudo-metric space structure whose underlying topological space structure
(definitionally) agrees which a pre-existing topology which is compatible with a given distance
function. -/
def PseudoMetricSpace.ofDistTopology {Œ± : Type u} [TopologicalSpace Œ±] (dist : Œ± ‚Üí Œ± ‚Üí ‚Ñù)
    (dist_self : ‚àÄ x : Œ±, dist x x = 0) (dist_comm : ‚àÄ x y : Œ±, dist x y = dist y x)
    (dist_triangle : ‚àÄ x y z : Œ±, dist x z ‚â§ dist x y + dist y z)
    (H : ‚àÄ s : Set Œ±, IsOpen s ‚Üî ‚àÄ x ‚àà s, ‚àÉ Œµ > 0, ‚àÄ y, dist x y < Œµ ‚Üí y ‚àà s) :
    PseudoMetricSpace Œ± :=
  { dist := dist
    dist_self := dist_self
    dist_comm := dist_comm
    dist_triangle := dist_triangle
    toUniformSpace :=
      (UniformSpace.ofDist dist dist_self dist_comm dist_triangle).replaceTopology <|
        TopologicalSpace.ext_iff.2 fun s ‚Ü¶ (H s).trans <| forall‚ÇÇ_congr fun x _ ‚Ü¶
          ((UniformSpace.hasBasis_ofFun (exists_gt (0 : ‚Ñù)) dist dist_self dist_comm dist_triangle
            UniformSpace.ofDist_aux).comap (Prod.mk x)).mem_iff.symm
    uniformity_dist := rfl
    toBornology := Bornology.ofDist dist dist_comm dist_triangle
    cobounded_sets := rfl }

@[simp]
theorem dist_self (x : Œ±) : dist x x = 0 :=
  PseudoMetricSpace.dist_self x

theorem dist_comm (x y : Œ±) : dist x y = dist y x :=
  PseudoMetricSpace.dist_comm x y

theorem edist_dist (x y : Œ±) : edist x y = ENNReal.ofReal (dist x y) :=
  PseudoMetricSpace.edist_dist x y

@[bound]
theorem dist_triangle (x y z : Œ±) : dist x z ‚â§ dist x y + dist y z :=
  PseudoMetricSpace.dist_triangle x y z

theorem dist_triangle_left (x y z : Œ±) : dist x y ‚â§ dist z x + dist z y := by
  rw [dist_comm z]; apply dist_triangle

theorem dist_triangle_right (x y z : Œ±) : dist x y ‚â§ dist x z + dist y z := by
  rw [dist_comm y]; apply dist_triangle

theorem dist_triangle4 (x y z w : Œ±) : dist x w ‚â§ dist x y + dist y z + dist z w :=
  calc
    dist x w ‚â§ dist x z + dist z w := dist_triangle x z w
    _ ‚â§ dist x y + dist y z + dist z w := by gcongr; apply dist_triangle x y z

theorem dist_triangle4_left (x‚ÇÅ y‚ÇÅ x‚ÇÇ y‚ÇÇ : Œ±) :
    dist x‚ÇÇ y‚ÇÇ ‚â§ dist x‚ÇÅ y‚ÇÅ + (dist x‚ÇÅ x‚ÇÇ + dist y‚ÇÅ y‚ÇÇ) := by
  rw [add_left_comm, dist_comm x‚ÇÅ, ‚Üê add_assoc]
  apply dist_triangle4

theorem dist_triangle4_right (x‚ÇÅ y‚ÇÅ x‚ÇÇ y‚ÇÇ : Œ±) :
    dist x‚ÇÅ y‚ÇÅ ‚â§ dist x‚ÇÅ x‚ÇÇ + dist y‚ÇÅ y‚ÇÇ + dist x‚ÇÇ y‚ÇÇ := by
  rw [add_right_comm, dist_comm y‚ÇÅ]
  apply dist_triangle4

theorem dist_triangle8 (a b c d e f g h : Œ±) : dist a h ‚â§ dist a b + dist b c + dist c d
    + dist d e + dist e f + dist f g + dist g h := by
  apply le_trans (dist_triangle4 a f g h)
  gcongr
  apply le_trans (dist_triangle4 a d e f)
  gcongr
  exact dist_triangle4 a b c d

theorem swap_dist : Function.swap (@dist Œ± _) = dist := by funext x y; exact dist_comm _ _

theorem abs_dist_sub_le (x y z : Œ±) : |dist x z - dist y z| ‚â§ dist x y :=
  abs_sub_le_iff.2
    ‚ü®sub_le_iff_le_add.2 (dist_triangle _ _ _), sub_le_iff_le_add.2 (dist_triangle_left _ _ _)‚ü©

@[simp, bound]
theorem dist_nonneg {x y : Œ±} : 0 ‚â§ dist x y :=
  dist_nonneg' dist dist_self dist_comm dist_triangle

namespace Mathlib.Meta.Positivity

open Lean Meta Qq Function

/-- Extension for the `positivity` tactic: distances are nonnegative. -/
@[positivity Dist.dist _ _]
def evalDist : PositivityExt where eval {u Œ±} _zŒ± _pŒ± e := do
  match u, Œ±, e with
  | 0, ~q(‚Ñù), ~q(@Dist.dist $Œ≤ $inst $a $b) =>
    let _inst ‚Üê synthInstanceQ q(PseudoMetricSpace $Œ≤)
    assertInstancesCommute
    pure (.nonnegative q(dist_nonneg))
  | _, _, _ => throwError "not dist"

end Mathlib.Meta.Positivity

example {x y : Œ±} : 0 ‚â§ dist x y := by positivity

@[simp] theorem abs_dist {a b : Œ±} : |dist a b| = dist a b := abs_of_nonneg dist_nonneg

/-- A version of `Dist` that takes value in `‚Ñù‚â•0`. -/
class NNDist (Œ± : Type*) where
  /-- Nonnegative distance between two points -/
  nndist : Œ± ‚Üí Œ± ‚Üí ‚Ñù‚â•0

export NNDist (nndist)

-- see Note [lower instance priority]
/-- Distance as a nonnegative real number. -/
instance (priority := 100) PseudoMetricSpace.toNNDist : NNDist Œ± :=
  ‚ü®fun a b => ‚ü®dist a b, dist_nonneg‚ü©‚ü©

/-- Express `dist` in terms of `nndist` -/
theorem dist_nndist (x y : Œ±) : dist x y = nndist x y := rfl

@[simp, norm_cast]
theorem coe_nndist (x y : Œ±) : ‚Üë(nndist x y) = dist x y := rfl

/-- Express `edist` in terms of `nndist` -/
theorem edist_nndist (x y : Œ±) : edist x y = nndist x y := by
  rw [edist_dist, dist_nndist, ENNReal.ofReal_coe_nnreal]

/-- Express `nndist` in terms of `edist` -/
theorem nndist_edist (x y : Œ±) : nndist x y = (edist x y).toNNReal := by
  simp [edist_nndist]

@[simp, norm_cast]
theorem coe_nnreal_ennreal_nndist (x y : Œ±) : ‚Üë(nndist x y) = edist x y :=
  (edist_nndist x y).symm

@[simp, norm_cast]
theorem edist_lt_coe {x y : Œ±} {c : ‚Ñù‚â•0} : edist x y < c ‚Üî nndist x y < c := by
  rw [edist_nndist, ENNReal.coe_lt_coe]

@[simp, norm_cast]
theorem edist_le_coe {x y : Œ±} {c : ‚Ñù‚â•0} : edist x y ‚â§ c ‚Üî nndist x y ‚â§ c := by
  rw [edist_nndist, ENNReal.coe_le_coe]

/-- In a pseudometric space, the extended distance is always finite -/
theorem edist_lt_top {Œ± : Type*} [PseudoMetricSpace Œ±] (x y : Œ±) : edist x y < ‚ä§ :=
  (edist_dist x y).symm ‚ñ∏ ENNReal.ofReal_lt_top

/-- In a pseudometric space, the extended distance is always finite -/
@[aesop (rule_sets := [finiteness]) safe apply]
theorem edist_ne_top (x y : Œ±) : edist x y ‚â† ‚ä§ :=
  (edist_lt_top x y).ne

/-- `nndist x x` vanishes -/
@[simp] theorem nndist_self (a : Œ±) : nndist a a = 0 := NNReal.coe_eq_zero.1 (dist_self a)

@[simp, norm_cast]
theorem dist_lt_coe {x y : Œ±} {c : ‚Ñù‚â•0} : dist x y < c ‚Üî nndist x y < c :=
  Iff.rfl

@[simp, norm_cast]
theorem dist_le_coe {x y : Œ±} {c : ‚Ñù‚â•0} : dist x y ‚â§ c ‚Üî nndist x y ‚â§ c :=
  Iff.rfl

@[simp]
theorem edist_lt_ofReal {x y : Œ±} {r : ‚Ñù} : edist x y < ENNReal.ofReal r ‚Üî dist x y < r := by
  rw [edist_dist, ENNReal.ofReal_lt_ofReal_iff_of_nonneg dist_nonneg]

@[simp]
theorem edist_le_ofReal {x y : Œ±} {r : ‚Ñù} (hr : 0 ‚â§ r) :
    edist x y ‚â§ ENNReal.ofReal r ‚Üî dist x y ‚â§ r := by
  rw [edist_dist, ENNReal.ofReal_le_ofReal_iff hr]

/-- Express `nndist` in terms of `dist` -/
theorem nndist_dist (x y : Œ±) : nndist x y = Real.toNNReal (dist x y) := by
  rw [dist_nndist, Real.toNNReal_coe]

theorem nndist_comm (x y : Œ±) : nndist x y = nndist y x := NNReal.eq <| dist_comm x y

/-- Triangle inequality for the nonnegative distance -/
theorem nndist_triangle (x y z : Œ±) : nndist x z ‚â§ nndist x y + nndist y z :=
  dist_triangle _ _ _

theorem nndist_triangle_left (x y z : Œ±) : nndist x y ‚â§ nndist z x + nndist z y :=
  dist_triangle_left _ _ _

theorem nndist_triangle_right (x y z : Œ±) : nndist x y ‚â§ nndist x z + nndist y z :=
  dist_triangle_right _ _ _

/-- Express `dist` in terms of `edist` -/
theorem dist_edist (x y : Œ±) : dist x y = (edist x y).toReal := by
  rw [edist_dist, ENNReal.toReal_ofReal dist_nonneg]

namespace Metric

-- instantiate pseudometric space as a topology
variable {x y z : Œ±} {Œ¥ Œµ Œµ‚ÇÅ Œµ‚ÇÇ : ‚Ñù} {s : Set Œ±}

/-- `ball x Œµ` is the set of all points `y` with `dist y x < Œµ` -/
def ball (x : Œ±) (Œµ : ‚Ñù) : Set Œ± :=
  { y | dist y x < Œµ }

@[simp]
theorem mem_ball : y ‚àà ball x Œµ ‚Üî dist y x < Œµ :=
  Iff.rfl

theorem mem_ball' : y ‚àà ball x Œµ ‚Üî dist x y < Œµ := by rw [dist_comm, mem_ball]

theorem pos_of_mem_ball (hy : y ‚àà ball x Œµ) : 0 < Œµ :=
  dist_nonneg.trans_lt hy

theorem mem_ball_self (h : 0 < Œµ) : x ‚àà ball x Œµ := by
  rwa [mem_ball, dist_self]

@[simp]
theorem nonempty_ball : (ball x Œµ).Nonempty ‚Üî 0 < Œµ :=
  ‚ü®fun ‚ü®_x, hx‚ü© => pos_of_mem_ball hx, fun h => ‚ü®x, mem_ball_self h‚ü©‚ü©

@[simp]
theorem ball_eq_empty : ball x Œµ = ‚àÖ ‚Üî Œµ ‚â§ 0 := by
  rw [‚Üê not_nonempty_iff_eq_empty, nonempty_ball, not_lt]

@[simp]
theorem ball_zero : ball x 0 = ‚àÖ := by rw [ball_eq_empty]

/-- If a point belongs to an open ball, then there is a strictly smaller radius whose ball also
contains it.

See also `exists_lt_subset_ball`. -/
theorem exists_lt_mem_ball_of_mem_ball (h : x ‚àà ball y Œµ) : ‚àÉ Œµ' < Œµ, x ‚àà ball y Œµ' := by
  simp only [mem_ball] at h ‚ä¢
  exact ‚ü®(dist x y + Œµ) / 2, by linarith, by linarith‚ü©

theorem ball_eq_ball (Œµ : ‚Ñù) (x : Œ±) :
    UniformSpace.ball x { p | dist p.2 p.1 < Œµ } = Metric.ball x Œµ :=
  rfl

theorem ball_eq_ball' (Œµ : ‚Ñù) (x : Œ±) :
    UniformSpace.ball x { p | dist p.1 p.2 < Œµ } = Metric.ball x Œµ := by
  ext
  simp [dist_comm, UniformSpace.ball]

@[simp]
theorem iUnion_ball_nat (x : Œ±) : ‚ãÉ n : ‚Ñï, ball x n = univ :=
  iUnion_eq_univ_iff.2 fun y => exists_nat_gt (dist y x)

@[simp]
theorem iUnion_ball_nat_succ (x : Œ±) : ‚ãÉ n : ‚Ñï, ball x (n + 1) = univ :=
  iUnion_eq_univ_iff.2 fun y => (exists_nat_gt (dist y x)).imp fun _ h => h.trans (lt_add_one _)

/-- `closedBall x Œµ` is the set of all points `y` with `dist y x ‚â§ Œµ` -/
def closedBall (x : Œ±) (Œµ : ‚Ñù) :=
  { y | dist y x ‚â§ Œµ }

@[simp] theorem mem_closedBall : y ‚àà closedBall x Œµ ‚Üî dist y x ‚â§ Œµ := Iff.rfl

theorem mem_closedBall' : y ‚àà closedBall x Œµ ‚Üî dist x y ‚â§ Œµ := by rw [dist_comm, mem_closedBall]

/-- `sphere x Œµ` is the set of all points `y` with `dist y x = Œµ` -/
def sphere (x : Œ±) (Œµ : ‚Ñù) := { y | dist y x = Œµ }

@[simp] theorem mem_sphere : y ‚àà sphere x Œµ ‚Üî dist y x = Œµ := Iff.rfl

theorem mem_sphere' : y ‚àà sphere x Œµ ‚Üî dist x y = Œµ := by rw [dist_comm, mem_sphere]

theorem ne_of_mem_sphere (h : y ‚àà sphere x Œµ) (hŒµ : Œµ ‚â† 0) : y ‚â† x :=
  ne_of_mem_of_not_mem h <| by simpa using hŒµ.symm

theorem nonneg_of_mem_sphere (hy : y ‚àà sphere x Œµ) : 0 ‚â§ Œµ :=
  dist_nonneg.trans_eq hy

@[simp]
theorem sphere_eq_empty_of_neg (hŒµ : Œµ < 0) : sphere x Œµ = ‚àÖ :=
  Set.eq_empty_iff_forall_notMem.mpr fun _y hy => (nonneg_of_mem_sphere hy).not_gt hŒµ

theorem sphere_eq_empty_of_subsingleton [Subsingleton Œ±] (hŒµ : Œµ ‚â† 0) : sphere x Œµ = ‚àÖ :=
  Set.eq_empty_iff_forall_notMem.mpr fun _ h => ne_of_mem_sphere h hŒµ (Subsingleton.elim _ _)

instance sphere_isEmpty_of_subsingleton [Subsingleton Œ±] [NeZero Œµ] : IsEmpty (sphere x Œµ) := by
  rw [sphere_eq_empty_of_subsingleton (NeZero.ne Œµ)]; infer_instance

theorem closedBall_eq_singleton_of_subsingleton [Subsingleton Œ±] (h : 0 ‚â§ Œµ) :
    closedBall x Œµ = {x} := by
  ext x'
  simpa [Subsingleton.allEq x x']

theorem ball_eq_singleton_of_subsingleton [Subsingleton Œ±] (h : 0 < Œµ) : ball x Œµ = {x} := by
  ext x'
  simpa [Subsingleton.allEq x x']

theorem mem_closedBall_self (h : 0 ‚â§ Œµ) : x ‚àà closedBall x Œµ := by
  rwa [mem_closedBall, dist_self]

@[simp]
theorem nonempty_closedBall : (closedBall x Œµ).Nonempty ‚Üî 0 ‚â§ Œµ :=
  ‚ü®fun ‚ü®_x, hx‚ü© => dist_nonneg.trans hx, fun h => ‚ü®x, mem_closedBall_self h‚ü©‚ü©

@[simp]
theorem closedBall_eq_empty : closedBall x Œµ = ‚àÖ ‚Üî Œµ < 0 := by
  rw [‚Üê not_nonempty_iff_eq_empty, nonempty_closedBall, not_le]

@[simp] alias ‚ü®_, closedBall_of_neg‚ü© := closedBall_eq_empty

/-- Closed balls and spheres coincide when the radius is non-positive -/
theorem closedBall_eq_sphere_of_nonpos (hŒµ : Œµ ‚â§ 0) : closedBall x Œµ = sphere x Œµ :=
  Set.ext fun _ => (hŒµ.trans dist_nonneg).ge_iff_eq'

theorem ball_subset_closedBall : ball x Œµ ‚äÜ closedBall x Œµ := fun _y hy =>
  mem_closedBall.2 (le_of_lt hy)

theorem sphere_subset_closedBall : sphere x Œµ ‚äÜ closedBall x Œµ := fun _ => le_of_eq

lemma sphere_subset_ball {r R : ‚Ñù} (h : r < R) : sphere x r ‚äÜ ball x R := fun _x hx ‚Ü¶
  (mem_sphere.1 hx).trans_lt h

theorem closedBall_disjoint_ball (h : Œ¥ + Œµ ‚â§ dist x y) : Disjoint (closedBall x Œ¥) (ball y Œµ) :=
  Set.disjoint_left.mpr fun _a ha1 ha2 =>
    (h.trans <| dist_triangle_left _ _ _).not_gt <| add_lt_add_of_le_of_lt ha1 ha2

theorem ball_disjoint_closedBall (h : Œ¥ + Œµ ‚â§ dist x y) : Disjoint (ball x Œ¥) (closedBall y Œµ) :=
  (closedBall_disjoint_ball <| by rwa [add_comm, dist_comm]).symm

theorem ball_disjoint_ball (h : Œ¥ + Œµ ‚â§ dist x y) : Disjoint (ball x Œ¥) (ball y Œµ) :=
  (closedBall_disjoint_ball h).mono_left ball_subset_closedBall

theorem closedBall_disjoint_closedBall (h : Œ¥ + Œµ < dist x y) :
    Disjoint (closedBall x Œ¥) (closedBall y Œµ) :=
  Set.disjoint_left.mpr fun _a ha1 ha2 =>
    h.not_ge <| (dist_triangle_left _ _ _).trans <| add_le_add ha1 ha2

theorem sphere_disjoint_ball : Disjoint (sphere x Œµ) (ball x Œµ) :=
  Set.disjoint_left.mpr fun _y hy‚ÇÅ hy‚ÇÇ => absurd hy‚ÇÅ <| ne_of_lt hy‚ÇÇ

@[simp]
theorem ball_union_sphere : ball x Œµ ‚à™ sphere x Œµ = closedBall x Œµ :=
  Set.ext fun _y => (@le_iff_lt_or_eq ‚Ñù _ _ _).symm

@[simp]
theorem sphere_union_ball : sphere x Œµ ‚à™ ball x Œµ = closedBall x Œµ := by
  rw [union_comm, ball_union_sphere]

@[simp]
theorem closedBall_diff_sphere : closedBall x Œµ \ sphere x Œµ = ball x Œµ := by
  rw [‚Üê ball_union_sphere, Set.union_diff_cancel_right sphere_disjoint_ball.symm.le_bot]

@[simp]
theorem closedBall_diff_ball : closedBall x Œµ \ ball x Œµ = sphere x Œµ := by
  rw [‚Üê ball_union_sphere, Set.union_diff_cancel_left sphere_disjoint_ball.symm.le_bot]

theorem mem_ball_comm : x ‚àà ball y Œµ ‚Üî y ‚àà ball x Œµ := by rw [mem_ball', mem_ball]

theorem mem_closedBall_comm : x ‚àà closedBall y Œµ ‚Üî y ‚àà closedBall x Œµ := by
  rw [mem_closedBall', mem_closedBall]

theorem mem_sphere_comm : x ‚àà sphere y Œµ ‚Üî y ‚àà sphere x Œµ := by rw [mem_sphere', mem_sphere]

@[gcongr]
theorem ball_subset_ball (h : Œµ‚ÇÅ ‚â§ Œµ‚ÇÇ) : ball x Œµ‚ÇÅ ‚äÜ ball x Œµ‚ÇÇ := fun _y yx =>
  lt_of_lt_of_le (mem_ball.1 yx) h

theorem closedBall_eq_bInter_ball : closedBall x Œµ = ‚ãÇ Œ¥ > Œµ, ball x Œ¥ := by
  ext y; rw [mem_closedBall, ‚Üê forall_gt_iff_le, mem_iInter‚ÇÇ]; rfl

theorem ball_subset_ball' (h : Œµ‚ÇÅ + dist x y ‚â§ Œµ‚ÇÇ) : ball x Œµ‚ÇÅ ‚äÜ ball y Œµ‚ÇÇ := fun z hz =>
  calc
    dist z y ‚â§ dist z x + dist x y := dist_triangle _ _ _
    _ < Œµ‚ÇÅ + dist x y := by gcongr; exact hz
    _ ‚â§ Œµ‚ÇÇ := h

@[gcongr]
theorem closedBall_subset_closedBall (h : Œµ‚ÇÅ ‚â§ Œµ‚ÇÇ) : closedBall x Œµ‚ÇÅ ‚äÜ closedBall x Œµ‚ÇÇ :=
  fun _y (yx : _ ‚â§ Œµ‚ÇÅ) => le_trans yx h

theorem closedBall_subset_closedBall' (h : Œµ‚ÇÅ + dist x y ‚â§ Œµ‚ÇÇ) :
    closedBall x Œµ‚ÇÅ ‚äÜ closedBall y Œµ‚ÇÇ := fun z hz =>
  calc
    dist z y ‚â§ dist z x + dist x y := dist_triangle _ _ _
    _ ‚â§ Œµ‚ÇÅ + dist x y := by gcongr; exact hz
    _ ‚â§ Œµ‚ÇÇ := h

theorem closedBall_subset_ball (h : Œµ‚ÇÅ < Œµ‚ÇÇ) : closedBall x Œµ‚ÇÅ ‚äÜ ball x Œµ‚ÇÇ :=
  fun y (yh : dist y x ‚â§ Œµ‚ÇÅ) => lt_of_le_of_lt yh h

theorem closedBall_subset_ball' (h : Œµ‚ÇÅ + dist x y < Œµ‚ÇÇ) :
    closedBall x Œµ‚ÇÅ ‚äÜ ball y Œµ‚ÇÇ := fun z hz =>
  calc
    dist z y ‚â§ dist z x + dist x y := dist_triangle _ _ _
    _ ‚â§ Œµ‚ÇÅ + dist x y := by gcongr; exact hz
    _ < Œµ‚ÇÇ := h

theorem dist_le_add_of_nonempty_closedBall_inter_closedBall
    (h : (closedBall x Œµ‚ÇÅ ‚à© closedBall y Œµ‚ÇÇ).Nonempty) : dist x y ‚â§ Œµ‚ÇÅ + Œµ‚ÇÇ :=
  let ‚ü®z, hz‚ü© := h
  calc
    dist x y ‚â§ dist z x + dist z y := dist_triangle_left _ _ _
    _ ‚â§ Œµ‚ÇÅ + Œµ‚ÇÇ := add_le_add hz.1 hz.2

theorem dist_lt_add_of_nonempty_closedBall_inter_ball (h : (closedBall x Œµ‚ÇÅ ‚à© ball y Œµ‚ÇÇ).Nonempty) :
    dist x y < Œµ‚ÇÅ + Œµ‚ÇÇ :=
  let ‚ü®z, hz‚ü© := h
  calc
    dist x y ‚â§ dist z x + dist z y := dist_triangle_left _ _ _
    _ < Œµ‚ÇÅ + Œµ‚ÇÇ := add_lt_add_of_le_of_lt hz.1 hz.2

theorem dist_lt_add_of_nonempty_ball_inter_closedBall (h : (ball x Œµ‚ÇÅ ‚à© closedBall y Œµ‚ÇÇ).Nonempty) :
    dist x y < Œµ‚ÇÅ + Œµ‚ÇÇ := by
  rw [inter_comm] at h
  rw [add_comm, dist_comm]
  exact dist_lt_add_of_nonempty_closedBall_inter_ball h

theorem dist_lt_add_of_nonempty_ball_inter_ball (h : (ball x Œµ‚ÇÅ ‚à© ball y Œµ‚ÇÇ).Nonempty) :
    dist x y < Œµ‚ÇÅ + Œµ‚ÇÇ :=
  dist_lt_add_of_nonempty_closedBall_inter_ball <|
    h.mono (inter_subset_inter ball_subset_closedBall Subset.rfl)

@[simp]
theorem iUnion_closedBall_nat (x : Œ±) : ‚ãÉ n : ‚Ñï, closedBall x n = univ :=
  iUnion_eq_univ_iff.2 fun y => exists_nat_ge (dist y x)

theorem iUnion_inter_closedBall_nat (s : Set Œ±) (x : Œ±) : ‚ãÉ n : ‚Ñï, s ‚à© closedBall x n = s := by
  rw [‚Üê inter_iUnion, iUnion_closedBall_nat, inter_univ]

theorem ball_subset (h : dist x y ‚â§ Œµ‚ÇÇ - Œµ‚ÇÅ) : ball x Œµ‚ÇÅ ‚äÜ ball y Œµ‚ÇÇ := fun z zx => by
  rw [‚Üê add_sub_cancel Œµ‚ÇÅ Œµ‚ÇÇ]
  exact lt_of_le_of_lt (dist_triangle z x y) (add_lt_add_of_lt_of_le zx h)

theorem ball_half_subset (y) (h : y ‚àà ball x (Œµ / 2)) : ball y (Œµ / 2) ‚äÜ ball x Œµ :=
  ball_subset <| by rw [sub_self_div_two]; exact le_of_lt h

theorem exists_ball_subset_ball (h : y ‚àà ball x Œµ) : ‚àÉ Œµ' > 0, ball y Œµ' ‚äÜ ball x Œµ :=
  ‚ü®_, sub_pos.2 h, ball_subset <| by rw [sub_sub_self]‚ü©

/-- If a property holds for all points in closed balls of arbitrarily large radii, then it holds for
all points. -/
theorem forall_of_forall_mem_closedBall (p : Œ± ‚Üí Prop) (x : Œ±)
    (H : ‚àÉ·∂† R : ‚Ñù in atTop, ‚àÄ y ‚àà closedBall x R, p y) (y : Œ±) : p y := by
  obtain ‚ü®R, hR, h‚ü© : ‚àÉ R ‚â• dist y x, ‚àÄ z : Œ±, z ‚àà closedBall x R ‚Üí p z :=
    frequently_iff.1 H (Ici_mem_atTop (dist y x))
  exact h _ hR

/-- If a property holds for all points in balls of arbitrarily large radii, then it holds for all
points. -/
theorem forall_of_forall_mem_ball (p : Œ± ‚Üí Prop) (x : Œ±)
    (H : ‚àÉ·∂† R : ‚Ñù in atTop, ‚àÄ y ‚àà ball x R, p y) (y : Œ±) : p y := by
  obtain ‚ü®R, hR, h‚ü© : ‚àÉ R > dist y x, ‚àÄ z : Œ±, z ‚àà ball x R ‚Üí p z :=
    frequently_iff.1 H (Ioi_mem_atTop (dist y x))
  exact h _ hR

theorem isBounded_iff {s : Set Œ±} :
    IsBounded s ‚Üî ‚àÉ C : ‚Ñù, ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí ‚àÄ ‚¶Éy‚¶Ñ, y ‚àà s ‚Üí dist x y ‚â§ C := by
  rw [isBounded_def, ‚Üê Filter.mem_sets, @PseudoMetricSpace.cobounded_sets Œ±, mem_setOf_eq,
    compl_compl]

theorem isBounded_iff_eventually {s : Set Œ±} :
    IsBounded s ‚Üî ‚àÄ·∂† C in atTop, ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí ‚àÄ ‚¶Éy‚¶Ñ, y ‚àà s ‚Üí dist x y ‚â§ C :=
  isBounded_iff.trans
    ‚ü®fun ‚ü®C, h‚ü© => eventually_atTop.2 ‚ü®C, fun _C' hC' _x hx _y hy => (h hx hy).trans hC'‚ü©,
      Eventually.exists‚ü©

theorem isBounded_iff_exists_ge {s : Set Œ±} (c : ‚Ñù) :
    IsBounded s ‚Üî ‚àÉ C, c ‚â§ C ‚àß ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí ‚àÄ ‚¶Éy‚¶Ñ, y ‚àà s ‚Üí dist x y ‚â§ C :=
  ‚ü®fun h => ((eventually_ge_atTop c).and (isBounded_iff_eventually.1 h)).exists, fun h =>
    isBounded_iff.2 <| h.imp fun _ => And.right‚ü©

theorem isBounded_iff_nndist {s : Set Œ±} :
    IsBounded s ‚Üî ‚àÉ C : ‚Ñù‚â•0, ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí ‚àÄ ‚¶Éy‚¶Ñ, y ‚àà s ‚Üí nndist x y ‚â§ C := by
  simp only [isBounded_iff_exists_ge 0, NNReal.exists, ‚Üê NNReal.coe_le_coe, ‚Üê dist_nndist,
    NNReal.coe_mk, exists_prop]

theorem toUniformSpace_eq :
    ‚ÄπPseudoMetricSpace Œ±‚Ä∫.toUniformSpace = .ofDist dist dist_self dist_comm dist_triangle :=
  UniformSpace.ext PseudoMetricSpace.uniformity_dist

theorem uniformity_basis_dist :
    (ùì§ Œ±).HasBasis (fun Œµ : ‚Ñù => 0 < Œµ) fun Œµ => { p : Œ± √ó Œ± | dist p.1 p.2 < Œµ } := by
  rw [toUniformSpace_eq]
  exact UniformSpace.hasBasis_ofFun (exists_gt _) _ _ _ _ _

/-- Given `f : Œ≤ ‚Üí ‚Ñù`, if `f` sends `{i | p i}` to a set of positive numbers
accumulating to zero, then `f i`-neighborhoods of the diagonal form a basis of `ùì§ Œ±`.

For specific bases see `uniformity_basis_dist`, `uniformity_basis_dist_inv_nat_succ`,
and `uniformity_basis_dist_inv_nat_pos`. -/
protected theorem mk_uniformity_basis {Œ≤ : Type*} {p : Œ≤ ‚Üí Prop} {f : Œ≤ ‚Üí ‚Ñù}
    (hf‚ÇÄ : ‚àÄ i, p i ‚Üí 0 < f i) (hf : ‚àÄ ‚¶ÉŒµ‚¶Ñ, 0 < Œµ ‚Üí ‚àÉ i, p i ‚àß f i ‚â§ Œµ) :
    (ùì§ Œ±).HasBasis p fun i => { p : Œ± √ó Œ± | dist p.1 p.2 < f i } := by
  refine ‚ü®fun s => uniformity_basis_dist.mem_iff.trans ?_‚ü©
  constructor
  ¬∑ rintro ‚ü®Œµ, Œµ‚ÇÄ, hŒµ‚ü©
    rcases hf Œµ‚ÇÄ with ‚ü®i, hi, H‚ü©
    exact ‚ü®i, hi, fun x (hx : _ < _) => hŒµ <| lt_of_lt_of_le hx H‚ü©
  ¬∑ exact fun ‚ü®i, hi, H‚ü© => ‚ü®f i, hf‚ÇÄ i hi, H‚ü©

theorem uniformity_basis_dist_rat :
    (ùì§ Œ±).HasBasis (fun r : ‚Ñö => 0 < r) fun r => { p : Œ± √ó Œ± | dist p.1 p.2 < r } :=
  Metric.mk_uniformity_basis (fun _ => Rat.cast_pos.2) fun _Œµ hŒµ =>
    let ‚ü®r, hr0, hrŒµ‚ü© := exists_rat_btwn hŒµ
    ‚ü®r, Rat.cast_pos.1 hr0, hrŒµ.le‚ü©

theorem uniformity_basis_dist_inv_nat_succ :
    (ùì§ Œ±).HasBasis (fun _ => True) fun n : ‚Ñï => { p : Œ± √ó Œ± | dist p.1 p.2 < 1 / (‚Üën + 1) } :=
  Metric.mk_uniformity_basis (fun n _ => div_pos zero_lt_one <| Nat.cast_add_one_pos n) fun _Œµ Œµ0 =>
    (exists_nat_one_div_lt Œµ0).imp fun _n hn => ‚ü®trivial, le_of_lt hn‚ü©

theorem uniformity_basis_dist_inv_nat_pos :
    (ùì§ Œ±).HasBasis (fun n : ‚Ñï => 0 < n) fun n : ‚Ñï => { p : Œ± √ó Œ± | dist p.1 p.2 < 1 / ‚Üën } :=
  Metric.mk_uniformity_basis (fun _ hn => div_pos zero_lt_one <| Nat.cast_pos.2 hn) fun _ Œµ0 =>
    let ‚ü®n, hn‚ü© := exists_nat_one_div_lt Œµ0
    ‚ü®n + 1, Nat.succ_pos n, mod_cast hn.le‚ü©

theorem uniformity_basis_dist_pow {r : ‚Ñù} (h0 : 0 < r) (h1 : r < 1) :
    (ùì§ Œ±).HasBasis (fun _ : ‚Ñï => True) fun n : ‚Ñï => { p : Œ± √ó Œ± | dist p.1 p.2 < r ^ n } :=
  Metric.mk_uniformity_basis (fun _ _ => pow_pos h0 _) fun _Œµ Œµ0 =>
    let ‚ü®n, hn‚ü© := exists_pow_lt_of_lt_one Œµ0 h1
    ‚ü®n, trivial, hn.le‚ü©

theorem uniformity_basis_dist_lt {R : ‚Ñù} (hR : 0 < R) :
    (ùì§ Œ±).HasBasis (fun r : ‚Ñù => 0 < r ‚àß r < R) fun r => { p : Œ± √ó Œ± | dist p.1 p.2 < r } :=
  Metric.mk_uniformity_basis (fun _ => And.left) fun r hr =>
    ‚ü®min r (R / 2), ‚ü®lt_min hr (half_pos hR), min_lt_iff.2 <| Or.inr (half_lt_self hR)‚ü©,
      min_le_left _ _‚ü©

/-- Given `f : Œ≤ ‚Üí ‚Ñù`, if `f` sends `{i | p i}` to a set of positive numbers
accumulating to zero, then closed neighborhoods of the diagonal of sizes `{f i | p i}`
form a basis of `ùì§ Œ±`.

Currently we have only one specific basis `uniformity_basis_dist_le` based on this constructor.
More can be easily added if needed in the future. -/
protected theorem mk_uniformity_basis_le {Œ≤ : Type*} {p : Œ≤ ‚Üí Prop} {f : Œ≤ ‚Üí ‚Ñù}
    (hf‚ÇÄ : ‚àÄ x, p x ‚Üí 0 < f x) (hf : ‚àÄ Œµ, 0 < Œµ ‚Üí ‚àÉ x, p x ‚àß f x ‚â§ Œµ) :
    (ùì§ Œ±).HasBasis p fun x => { p : Œ± √ó Œ± | dist p.1 p.2 ‚â§ f x } := by
  refine ‚ü®fun s => uniformity_basis_dist.mem_iff.trans ?_‚ü©
  constructor
  ¬∑ rintro ‚ü®Œµ, Œµ‚ÇÄ, hŒµ‚ü©
    rcases exists_between Œµ‚ÇÄ with ‚ü®Œµ', hŒµ'‚ü©
    rcases hf Œµ' hŒµ'.1 with ‚ü®i, hi, H‚ü©
    exact ‚ü®i, hi, fun x (hx : _ ‚â§ _) => hŒµ <| lt_of_le_of_lt (le_trans hx H) hŒµ'.2‚ü©
  ¬∑ exact fun ‚ü®i, hi, H‚ü© => ‚ü®f i, hf‚ÇÄ i hi, fun x (hx : _ < _) => H (mem_setOf.2 hx.le)‚ü©

/-- Constant size closed neighborhoods of the diagonal form a basis
of the uniformity filter. -/
theorem uniformity_basis_dist_le :
    (ùì§ Œ±).HasBasis ((0 : ‚Ñù) < ¬∑) fun Œµ => { p : Œ± √ó Œ± | dist p.1 p.2 ‚â§ Œµ } :=
  Metric.mk_uniformity_basis_le (fun _ => id) fun Œµ Œµ‚ÇÄ => ‚ü®Œµ, Œµ‚ÇÄ, le_refl Œµ‚ü©

theorem uniformity_basis_dist_le_pow {r : ‚Ñù} (h0 : 0 < r) (h1 : r < 1) :
    (ùì§ Œ±).HasBasis (fun _ : ‚Ñï => True) fun n : ‚Ñï => { p : Œ± √ó Œ± | dist p.1 p.2 ‚â§ r ^ n } :=
  Metric.mk_uniformity_basis_le (fun _ _ => pow_pos h0 _) fun _Œµ Œµ0 =>
    let ‚ü®n, hn‚ü© := exists_pow_lt_of_lt_one Œµ0 h1
    ‚ü®n, trivial, hn.le‚ü©

theorem mem_uniformity_dist {s : Set (Œ± √ó Œ±)} :
    s ‚àà ùì§ Œ± ‚Üî ‚àÉ Œµ > 0, ‚àÄ ‚¶Éa b : Œ±‚¶Ñ, dist a b < Œµ ‚Üí (a, b) ‚àà s :=
  uniformity_basis_dist.mem_uniformity_iff

/-- A constant size neighborhood of the diagonal is an entourage. -/
theorem dist_mem_uniformity {Œµ : ‚Ñù} (Œµ0 : 0 < Œµ) : { p : Œ± √ó Œ± | dist p.1 p.2 < Œµ } ‚àà ùì§ Œ± :=
  mem_uniformity_dist.2 ‚ü®Œµ, Œµ0, fun _ _ ‚Ü¶ id‚ü©

theorem uniformContinuous_iff [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} :
    UniformContinuous f ‚Üî ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ ‚¶Éa b : Œ±‚¶Ñ, dist a b < Œ¥ ‚Üí dist (f a) (f b) < Œµ :=
  uniformity_basis_dist.uniformContinuous_iff uniformity_basis_dist

theorem uniformContinuousOn_iff [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ±} :
    UniformContinuousOn f s ‚Üî
      ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, dist x y < Œ¥ ‚Üí dist (f x) (f y) < Œµ :=
  Metric.uniformity_basis_dist.uniformContinuousOn_iff Metric.uniformity_basis_dist

theorem uniformContinuousOn_iff_le [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ±} :
    UniformContinuousOn f s ‚Üî
      ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, dist x y ‚â§ Œ¥ ‚Üí dist (f x) (f y) ‚â§ Œµ :=
  Metric.uniformity_basis_dist_le.uniformContinuousOn_iff Metric.uniformity_basis_dist_le

theorem nhds_basis_ball : (ùìù x).HasBasis (0 < ¬∑) (ball x) :=
  nhds_basis_uniformity uniformity_basis_dist

theorem mem_nhds_iff : s ‚àà ùìù x ‚Üî ‚àÉ Œµ > 0, ball x Œµ ‚äÜ s :=
  nhds_basis_ball.mem_iff

theorem eventually_nhds_iff {p : Œ± ‚Üí Prop} :
    (‚àÄ·∂† y in ùìù x, p y) ‚Üî ‚àÉ Œµ > 0, ‚àÄ ‚¶Éy‚¶Ñ, dist y x < Œµ ‚Üí p y :=
  mem_nhds_iff

theorem eventually_nhds_iff_ball {p : Œ± ‚Üí Prop} :
    (‚àÄ·∂† y in ùìù x, p y) ‚Üî ‚àÉ Œµ > 0, ‚àÄ y ‚àà ball x Œµ, p y :=
  mem_nhds_iff

/-- A version of `Filter.eventually_prod_iff` where the first filter consists of neighborhoods
in a pseudo-metric space. -/
theorem eventually_nhds_prod_iff {f : Filter Œπ} {x‚ÇÄ : Œ±} {p : Œ± √ó Œπ ‚Üí Prop} :
    (‚àÄ·∂† x in ùìù x‚ÇÄ √óÀ¢ f, p x) ‚Üî ‚àÉ Œµ > (0 : ‚Ñù), ‚àÉ pa : Œπ ‚Üí Prop, (‚àÄ·∂† i in f, pa i) ‚àß
      ‚àÄ ‚¶Éx‚¶Ñ, dist x x‚ÇÄ < Œµ ‚Üí ‚àÄ ‚¶Éi‚¶Ñ, pa i ‚Üí p (x, i) := by
  refine (nhds_basis_ball.prod f.basis_sets).eventually_iff.trans ?_
  simp only [Prod.exists, forall_prod_set, id, mem_ball, and_assoc, exists_and_left]
  rfl

/-- A version of `Filter.eventually_prod_iff` where the second filter consists of neighborhoods
in a pseudo-metric space. -/
theorem eventually_prod_nhds_iff {f : Filter Œπ} {x‚ÇÄ : Œ±} {p : Œπ √ó Œ± ‚Üí Prop} :
    (‚àÄ·∂† x in f √óÀ¢ ùìù x‚ÇÄ, p x) ‚Üî ‚àÉ pa : Œπ ‚Üí Prop, (‚àÄ·∂† i in f, pa i) ‚àß
      ‚àÉ Œµ > 0, ‚àÄ ‚¶Éi‚¶Ñ, pa i ‚Üí ‚àÄ ‚¶Éx‚¶Ñ, dist x x‚ÇÄ < Œµ ‚Üí p (i, x) := by
  rw [eventually_swap_iff, Metric.eventually_nhds_prod_iff]
  constructor <;>
    ¬∑ rintro ‚ü®a1, a2, a3, a4, a5‚ü©
      exact ‚ü®a3, a4, a1, a2, fun _ b1 b2 b3 => a5 b3 b1‚ü©

theorem nhds_basis_closedBall : (ùìù x).HasBasis (fun Œµ : ‚Ñù => 0 < Œµ) (closedBall x) :=
  nhds_basis_uniformity uniformity_basis_dist_le

theorem nhds_basis_ball_inv_nat_succ :
    (ùìù x).HasBasis (fun _ => True) fun n : ‚Ñï => ball x (1 / (‚Üën + 1)) :=
  nhds_basis_uniformity uniformity_basis_dist_inv_nat_succ

theorem nhds_basis_ball_inv_nat_pos :
    (ùìù x).HasBasis (fun n => 0 < n) fun n : ‚Ñï => ball x (1 / ‚Üën) :=
  nhds_basis_uniformity uniformity_basis_dist_inv_nat_pos

theorem nhds_basis_ball_pow {r : ‚Ñù} (h0 : 0 < r) (h1 : r < 1) :
    (ùìù x).HasBasis (fun _ => True) fun n : ‚Ñï => ball x (r ^ n) :=
  nhds_basis_uniformity (uniformity_basis_dist_pow h0 h1)

theorem nhds_basis_closedBall_pow {r : ‚Ñù} (h0 : 0 < r) (h1 : r < 1) :
    (ùìù x).HasBasis (fun _ => True) fun n : ‚Ñï => closedBall x (r ^ n) :=
  nhds_basis_uniformity (uniformity_basis_dist_le_pow h0 h1)

theorem isOpen_iff : IsOpen s ‚Üî ‚àÄ x ‚àà s, ‚àÉ Œµ > 0, ball x Œµ ‚äÜ s := by
  simp only [isOpen_iff_mem_nhds, mem_nhds_iff]

@[simp] theorem isOpen_ball : IsOpen (ball x Œµ) :=
  isOpen_iff.2 fun _ => exists_ball_subset_ball

theorem ball_mem_nhds (x : Œ±) {Œµ : ‚Ñù} (Œµ0 : 0 < Œµ) : ball x Œµ ‚àà ùìù x :=
  isOpen_ball.mem_nhds (mem_ball_self Œµ0)

theorem closedBall_mem_nhds (x : Œ±) {Œµ : ‚Ñù} (Œµ0 : 0 < Œµ) : closedBall x Œµ ‚àà ùìù x :=
  mem_of_superset (ball_mem_nhds x Œµ0) ball_subset_closedBall

theorem closedBall_mem_nhds_of_mem {x c : Œ±} {Œµ : ‚Ñù} (h : x ‚àà ball c Œµ) : closedBall c Œµ ‚àà ùìù x :=
  mem_of_superset (isOpen_ball.mem_nhds h) ball_subset_closedBall

theorem nhdsWithin_basis_ball {s : Set Œ±} :
    (ùìù[s] x).HasBasis (fun Œµ : ‚Ñù => 0 < Œµ) fun Œµ => ball x Œµ ‚à© s :=
  nhdsWithin_hasBasis nhds_basis_ball s

theorem mem_nhdsWithin_iff {t : Set Œ±} : s ‚àà ùìù[t] x ‚Üî ‚àÉ Œµ > 0, ball x Œµ ‚à© t ‚äÜ s :=
  nhdsWithin_basis_ball.mem_iff

theorem tendsto_nhdsWithin_nhdsWithin [PseudoMetricSpace Œ≤] {t : Set Œ≤} {f : Œ± ‚Üí Œ≤} {a b} :
    Tendsto f (ùìù[s] a) (ùìù[t] b) ‚Üî
      ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ ‚¶Éx : Œ±‚¶Ñ, x ‚àà s ‚Üí dist x a < Œ¥ ‚Üí f x ‚àà t ‚àß dist (f x) b < Œµ :=
  (nhdsWithin_basis_ball.tendsto_iff nhdsWithin_basis_ball).trans <| by
    simp only [inter_comm _ s, inter_comm _ t, mem_inter_iff, and_imp, gt_iff_lt, mem_ball]

theorem tendsto_nhdsWithin_nhds [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} {a b} :
    Tendsto f (ùìù[s] a) (ùìù b) ‚Üî
      ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ ‚¶Éx : Œ±‚¶Ñ, x ‚àà s ‚Üí dist x a < Œ¥ ‚Üí dist (f x) b < Œµ := by
  rw [‚Üê nhdsWithin_univ b, tendsto_nhdsWithin_nhdsWithin]
  simp only [mem_univ, true_and]

theorem tendsto_nhds_nhds [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} {a b} :
    Tendsto f (ùìù a) (ùìù b) ‚Üî ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ ‚¶Éx : Œ±‚¶Ñ, dist x a < Œ¥ ‚Üí dist (f x) b < Œµ :=
  nhds_basis_ball.tendsto_iff nhds_basis_ball

theorem continuousAt_iff [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} {a : Œ±} :
    ContinuousAt f a ‚Üî ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ ‚¶Éx : Œ±‚¶Ñ, dist x a < Œ¥ ‚Üí dist (f x) (f a) < Œµ := by
  rw [ContinuousAt, tendsto_nhds_nhds]

theorem continuousWithinAt_iff [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} {a : Œ±} {s : Set Œ±} :
    ContinuousWithinAt f s a ‚Üî
      ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ ‚¶Éx : Œ±‚¶Ñ, x ‚àà s ‚Üí dist x a < Œ¥ ‚Üí dist (f x) (f a) < Œµ := by
  rw [ContinuousWithinAt, tendsto_nhdsWithin_nhds]

theorem continuousOn_iff [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ±} :
    ContinuousOn f s ‚Üî ‚àÄ b ‚àà s, ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ a ‚àà s, dist a b < Œ¥ ‚Üí dist (f a) (f b) < Œµ := by
  simp [ContinuousOn, continuousWithinAt_iff]

theorem continuous_iff [PseudoMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} :
    Continuous f ‚Üî ‚àÄ b, ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ a, dist a b < Œ¥ ‚Üí dist (f a) (f b) < Œµ :=
  continuous_iff_continuousAt.trans <| forall_congr' fun _ => tendsto_nhds_nhds

theorem tendsto_nhds {f : Filter Œ≤} {u : Œ≤ ‚Üí Œ±} {a : Œ±} :
    Tendsto u f (ùìù a) ‚Üî ‚àÄ Œµ > 0, ‚àÄ·∂† x in f, dist (u x) a < Œµ :=
  nhds_basis_ball.tendsto_right_iff

theorem continuousAt_iff' [TopologicalSpace Œ≤] {f : Œ≤ ‚Üí Œ±} {b : Œ≤} :
    ContinuousAt f b ‚Üî ‚àÄ Œµ > 0, ‚àÄ·∂† x in ùìù b, dist (f x) (f b) < Œµ := by
  rw [ContinuousAt, tendsto_nhds]

theorem continuousWithinAt_iff' [TopologicalSpace Œ≤] {f : Œ≤ ‚Üí Œ±} {b : Œ≤} {s : Set Œ≤} :
    ContinuousWithinAt f s b ‚Üî ‚àÄ Œµ > 0, ‚àÄ·∂† x in ùìù[s] b, dist (f x) (f b) < Œµ := by
  rw [ContinuousWithinAt, tendsto_nhds]

theorem continuousOn_iff' [TopologicalSpace Œ≤] {f : Œ≤ ‚Üí Œ±} {s : Set Œ≤} :
    ContinuousOn f s ‚Üî ‚àÄ b ‚àà s, ‚àÄ Œµ > 0, ‚àÄ·∂† x in ùìù[s] b, dist (f x) (f b) < Œµ := by
  simp [ContinuousOn, continuousWithinAt_iff']

theorem continuous_iff' [TopologicalSpace Œ≤] {f : Œ≤ ‚Üí Œ±} :
    Continuous f ‚Üî ‚àÄ (a), ‚àÄ Œµ > 0, ‚àÄ·∂† x in ùìù a, dist (f x) (f a) < Œµ :=
  continuous_iff_continuousAt.trans <| forall_congr' fun _ => tendsto_nhds

theorem tendsto_atTop [Nonempty Œ≤] [SemilatticeSup Œ≤] {u : Œ≤ ‚Üí Œ±} {a : Œ±} :
    Tendsto u atTop (ùìù a) ‚Üî ‚àÄ Œµ > 0, ‚àÉ N, ‚àÄ n ‚â• N, dist (u n) a < Œµ :=
  (atTop_basis.tendsto_iff nhds_basis_ball).trans <| by
    simp only [true_and, mem_ball, mem_Ici]

/-- A variant of `tendsto_atTop` that
uses `‚àÉ N, ‚àÄ n > N, ...` rather than `‚àÉ N, ‚àÄ n ‚â• N, ...`
-/
theorem tendsto_atTop' [Nonempty Œ≤] [SemilatticeSup Œ≤] [NoMaxOrder Œ≤] {u : Œ≤ ‚Üí Œ±} {a : Œ±} :
    Tendsto u atTop (ùìù a) ‚Üî ‚àÄ Œµ > 0, ‚àÉ N, ‚àÄ n > N, dist (u n) a < Œµ :=
  (atTop_basis_Ioi.tendsto_iff nhds_basis_ball).trans <| by
    simp only [true_and, gt_iff_lt, mem_Ioi, mem_ball]

theorem isOpen_singleton_iff {Œ± : Type*} [PseudoMetricSpace Œ±] {x : Œ±} :
    IsOpen ({x} : Set Œ±) ‚Üî ‚àÉ Œµ > 0, ‚àÄ y, dist y x < Œµ ‚Üí y = x := by
  simp [isOpen_iff, subset_singleton_iff, mem_ball]

theorem _root_.Dense.exists_dist_lt {s : Set Œ±} (hs : Dense s) (x : Œ±) {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) :
    ‚àÉ y ‚àà s, dist x y < Œµ := by
  have : (ball x Œµ).Nonempty := by simp [hŒµ]
  simpa only [mem_ball'] using hs.exists_mem_open isOpen_ball this

nonrec theorem _root_.DenseRange.exists_dist_lt {Œ≤ : Type*} {f : Œ≤ ‚Üí Œ±} (hf : DenseRange f) (x : Œ±)
    {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) : ‚àÉ y, dist x (f y) < Œµ :=
  exists_range_iff.1 (hf.exists_dist_lt x hŒµ)

/-- (Pseudo) metric space has discrete `UniformSpace` structure
iff the distances between distinct points are uniformly bounded away from zero. -/
protected lemma uniformSpace_eq_bot :
    ‚ÄπPseudoMetricSpace Œ±‚Ä∫.toUniformSpace = ‚ä• ‚Üî
      ‚àÉ r : ‚Ñù, 0 < r ‚àß Pairwise (r ‚â§ dist ¬∑ ¬∑ : Œ± ‚Üí Œ± ‚Üí Prop) := by
  simp only [uniformity_basis_dist.uniformSpace_eq_bot, mem_setOf_eq, not_lt]

end Metric

open Metric

/-- If the distances between distinct points in a (pseudo) metric space
are uniformly bounded away from zero, then the space has discrete topology. -/
lemma DiscreteTopology.of_forall_le_dist {Œ±} [PseudoMetricSpace Œ±] {r : ‚Ñù} (hpos : 0 < r)
    (hr : Pairwise (r ‚â§ dist ¬∑ ¬∑ : Œ± ‚Üí Œ± ‚Üí Prop)) : DiscreteTopology Œ± :=
  ‚ü®by rw [Metric.uniformSpace_eq_bot.2 ‚ü®r, hpos, hr‚ü©, UniformSpace.toTopologicalSpace_bot]‚ü©

/- Instantiate a pseudometric space as a pseudoemetric space. Before we can state the instance,
we need to show that the uniform structure coming from the edistance and the
distance coincide. -/

theorem Metric.uniformity_edist_aux {Œ±} (d : Œ± ‚Üí Œ± ‚Üí ‚Ñù‚â•0) :
    ‚®Ö Œµ > (0 : ‚Ñù), ùìü { p : Œ± √ó Œ± | ‚Üë(d p.1 p.2) < Œµ } =
      ‚®Ö Œµ > (0 : ‚Ñù‚â•0‚àû), ùìü { p : Œ± √ó Œ± | ‚Üë(d p.1 p.2) < Œµ } := by
  simp only [le_antisymm_iff, le_iInf_iff, le_principal_iff]
  refine ‚ü®fun Œµ hŒµ => ?_, fun Œµ hŒµ => ?_‚ü©
  ¬∑ rcases ENNReal.lt_iff_exists_nnreal_btwn.1 hŒµ with ‚ü®Œµ', Œµ'0, Œµ'Œµ‚ü©
    refine mem_iInf_of_mem (Œµ' : ‚Ñù) (mem_iInf_of_mem (ENNReal.coe_pos.1 Œµ'0) ?_)
    exact fun x hx => lt_trans (ENNReal.coe_lt_coe.2 hx) Œµ'Œµ
  ¬∑ lift Œµ to ‚Ñù‚â•0 using le_of_lt hŒµ
    refine mem_iInf_of_mem (Œµ : ‚Ñù‚â•0‚àû) (mem_iInf_of_mem (ENNReal.coe_pos.2 hŒµ) ?_)
    exact fun _ => ENNReal.coe_lt_coe.1

theorem Metric.uniformity_edist : ùì§ Œ± = ‚®Ö Œµ > 0, ùìü { p : Œ± √ó Œ± | edist p.1 p.2 < Œµ } := by
  simp only [PseudoMetricSpace.uniformity_dist, dist_nndist, edist_nndist,
    Metric.uniformity_edist_aux]

-- see Note [lower instance priority]
/-- A pseudometric space induces a pseudoemetric space -/
instance (priority := 100) PseudoMetricSpace.toPseudoEMetricSpace : PseudoEMetricSpace Œ± :=
  { ‚ÄπPseudoMetricSpace Œ±‚Ä∫ with
    edist_self := by simp [edist_dist]
    edist_comm := fun _ _ => by simp only [edist_dist, dist_comm]
    edist_triangle := fun x y z => by
      simp only [edist_dist, ‚Üê ENNReal.ofReal_add, dist_nonneg]
      rw [ENNReal.ofReal_le_ofReal_iff _]
      ¬∑ exact dist_triangle _ _ _
      ¬∑ simpa using add_le_add (dist_nonneg : 0 ‚â§ dist x y) dist_nonneg
    uniformity_edist := Metric.uniformity_edist }

/-- In a pseudometric space, an open ball of infinite radius is the whole space -/
theorem Metric.eball_top_eq_univ (x : Œ±) : EMetric.ball x ‚àû = Set.univ :=
  Set.eq_univ_iff_forall.mpr fun y => edist_lt_top y x

/-- Balls defined using the distance or the edistance coincide -/
@[simp]
theorem Metric.emetric_ball {x : Œ±} {Œµ : ‚Ñù} : EMetric.ball x (ENNReal.ofReal Œµ) = ball x Œµ := by
  ext y
  simp only [EMetric.mem_ball, mem_ball, edist_dist]
  exact ENNReal.ofReal_lt_ofReal_iff_of_nonneg dist_nonneg

/-- Balls defined using the distance or the edistance coincide -/
@[simp]
theorem Metric.emetric_ball_nnreal {x : Œ±} {Œµ : ‚Ñù‚â•0} : EMetric.ball x Œµ = ball x Œµ := by
  rw [‚Üê Metric.emetric_ball]
  simp

/-- Closed balls defined using the distance or the edistance coincide -/
theorem Metric.emetric_closedBall {x : Œ±} {Œµ : ‚Ñù} (h : 0 ‚â§ Œµ) :
    EMetric.closedBall x (ENNReal.ofReal Œµ) = closedBall x Œµ := by
  ext y; simp [edist_le_ofReal h]

/-- Closed balls defined using the distance or the edistance coincide -/
@[simp]
theorem Metric.emetric_closedBall_nnreal {x : Œ±} {Œµ : ‚Ñù‚â•0} :
    EMetric.closedBall x Œµ = closedBall x Œµ := by
  rw [‚Üê Metric.emetric_closedBall Œµ.coe_nonneg, ENNReal.ofReal_coe_nnreal]

@[simp]
theorem Metric.emetric_ball_top (x : Œ±) : EMetric.ball x ‚ä§ = univ :=
  eq_univ_of_forall fun _ => edist_lt_top _ _

/-- Build a new pseudometric space from an old one where the bundled uniform structure is provably
(but typically non-definitionaly) equal to some given uniform structure.
See Note [forgetful inheritance].
See Note [reducible non-instances].
-/
abbrev PseudoMetricSpace.replaceUniformity {Œ±} [U : UniformSpace Œ±] (m : PseudoMetricSpace Œ±)
    (H : ùì§[U] = ùì§[PseudoEMetricSpace.toUniformSpace]) : PseudoMetricSpace Œ± :=
  { m with
    toUniformSpace := U
    uniformity_dist := H.trans PseudoMetricSpace.uniformity_dist }

theorem PseudoMetricSpace.replaceUniformity_eq {Œ±} [U : UniformSpace Œ±] (m : PseudoMetricSpace Œ±)
    (H : ùì§[U] = ùì§[PseudoEMetricSpace.toUniformSpace]) : m.replaceUniformity H = m := by
  ext
  rfl

-- ensure that the bornology is unchanged when replacing the uniformity.
example {Œ±} [U : UniformSpace Œ±] (m : PseudoMetricSpace Œ±)
    (H : ùì§[U] = ùì§[PseudoEMetricSpace.toUniformSpace]) :
    (PseudoMetricSpace.replaceUniformity m H).toBornology = m.toBornology := by
  with_reducible_and_instances rfl

/-- Build a new pseudo metric space from an old one where the bundled topological structure is
provably (but typically non-definitionaly) equal to some given topological structure.
See Note [forgetful inheritance].
See Note [reducible non-instances].
-/
abbrev PseudoMetricSpace.replaceTopology {Œ≥} [U : TopologicalSpace Œ≥] (m : PseudoMetricSpace Œ≥)
    (H : U = m.toUniformSpace.toTopologicalSpace) : PseudoMetricSpace Œ≥ :=
  @PseudoMetricSpace.replaceUniformity Œ≥ (m.toUniformSpace.replaceTopology H) m rfl

theorem PseudoMetricSpace.replaceTopology_eq {Œ≥} [U : TopologicalSpace Œ≥] (m : PseudoMetricSpace Œ≥)
    (H : U = m.toUniformSpace.toTopologicalSpace) : m.replaceTopology H = m := by
  ext
  rfl

/-- One gets a pseudometric space from an emetric space if the edistance
is everywhere finite, by pushing the edistance to reals. We set it up so that the edist and the
uniformity are defeq in the pseudometric space and the pseudoemetric space. In this definition, the
distance is given separately, to be able to prescribe some expression which is not defeq to the
push-forward of the edistance to reals. See note [reducible non-instances]. -/
abbrev PseudoEMetricSpace.toPseudoMetricSpaceOfDist {Œ± : Type u} [e : PseudoEMetricSpace Œ±]
    (dist : Œ± ‚Üí Œ± ‚Üí ‚Ñù) (edist_ne_top : ‚àÄ x y : Œ±, edist x y ‚â† ‚ä§)
    (h : ‚àÄ x y, dist x y = ENNReal.toReal (edist x y)) : PseudoMetricSpace Œ± where
  dist := dist
  dist_self x := by simp [h]
  dist_comm x y := by simp [h, edist_comm]
  dist_triangle x y z := by
    simp only [h]
    exact ENNReal.toReal_le_add (edist_triangle _ _ _) (edist_ne_top _ _) (edist_ne_top _ _)
  edist := edist
  edist_dist _ _ := by simp only [h, ENNReal.ofReal_toReal (edist_ne_top _ _)]
  toUniformSpace := e.toUniformSpace
  uniformity_dist := e.uniformity_edist.trans <| by
    simpa only [ENNReal.coe_toNNReal (edist_ne_top _ _), h]
      using (Metric.uniformity_edist_aux fun x y : Œ± => (edist x y).toNNReal).symm

/-- One gets a pseudometric space from an emetric space if the edistance
is everywhere finite, by pushing the edistance to reals. We set it up so that the edist and the
uniformity are defeq in the pseudometric space and the emetric space. -/
abbrev PseudoEMetricSpace.toPseudoMetricSpace {Œ± : Type u} [PseudoEMetricSpace Œ±]
    (h : ‚àÄ x y : Œ±, edist x y ‚â† ‚ä§) : PseudoMetricSpace Œ± :=
  PseudoEMetricSpace.toPseudoMetricSpaceOfDist (fun x y => ENNReal.toReal (edist x y)) h fun _ _ =>
    rfl

/-- Build a new pseudometric space from an old one where the bundled bornology structure is provably
(but typically non-definitionaly) equal to some given bornology structure.
See Note [forgetful inheritance].
See Note [reducible non-instances].
-/
abbrev PseudoMetricSpace.replaceBornology {Œ±} [B : Bornology Œ±] (m : PseudoMetricSpace Œ±)
    (H : ‚àÄ s, @IsBounded _ B s ‚Üî @IsBounded _ PseudoMetricSpace.toBornology s) :
    PseudoMetricSpace Œ± :=
  { m with
    toBornology := B
    cobounded_sets := Set.ext <| compl_surjective.forall.2 fun s =>
        (H s).trans <| by rw [isBounded_iff, mem_setOf_eq, compl_compl] }

theorem PseudoMetricSpace.replaceBornology_eq {Œ±} [m : PseudoMetricSpace Œ±] [B : Bornology Œ±]
    (H : ‚àÄ s, @IsBounded _ B s ‚Üî @IsBounded _ PseudoMetricSpace.toBornology s) :
    PseudoMetricSpace.replaceBornology _ H = m := by
  ext
  rfl

-- ensure that the uniformity is unchanged when replacing the bornology.
example {Œ±} [B : Bornology Œ±] (m : PseudoMetricSpace Œ±)
    (H : ‚àÄ s, @IsBounded _ B s ‚Üî @IsBounded _ PseudoMetricSpace.toBornology s) :
    (PseudoMetricSpace.replaceBornology m H).toUniformSpace = m.toUniformSpace := by
  with_reducible_and_instances rfl

section Real

/-- Instantiate the reals as a pseudometric space. -/
instance Real.pseudoMetricSpace : PseudoMetricSpace ‚Ñù where
  dist x y := |x - y|
  dist_self := by simp [abs_zero]
  dist_comm _ _ := abs_sub_comm _ _
  dist_triangle _ _ _ := abs_sub_le _ _ _

theorem Real.dist_eq (x y : ‚Ñù) : dist x y = |x - y| := rfl

theorem Real.nndist_eq (x y : ‚Ñù) : nndist x y = Real.nnabs (x - y) := rfl

theorem Real.nndist_eq' (x y : ‚Ñù) : nndist x y = Real.nnabs (y - x) :=
  nndist_comm _ _

theorem Real.dist_0_eq_abs (x : ‚Ñù) : dist x 0 = |x| := by simp [Real.dist_eq]

theorem Real.sub_le_dist (x y : ‚Ñù) : x - y ‚â§ dist x y := by
  rw [Real.dist_eq, le_abs]
  exact Or.inl (le_refl _)

theorem Real.ball_eq_Ioo (x r : ‚Ñù) : ball x r = Ioo (x - r) (x + r) :=
  Set.ext fun y => by
    rw [mem_ball, dist_comm, Real.dist_eq, abs_sub_lt_iff, mem_Ioo, ‚Üê sub_lt_iff_lt_add',
      sub_lt_comm]

theorem Real.closedBall_eq_Icc {x r : ‚Ñù} : closedBall x r = Icc (x - r) (x + r) := by
  ext y
  rw [mem_closedBall, dist_comm, Real.dist_eq, abs_sub_le_iff, mem_Icc, ‚Üê sub_le_iff_le_add',
    sub_le_comm]

theorem Real.Ioo_eq_ball (x y : ‚Ñù) : Ioo x y = ball ((x + y) / 2) ((y - x) / 2) := by
  rw [Real.ball_eq_Ioo, ‚Üê sub_div, add_comm, ‚Üê sub_add, add_sub_cancel_left, add_self_div_two,
    ‚Üê add_div, add_assoc, add_sub_cancel, add_self_div_two]

theorem Real.Icc_eq_closedBall (x y : ‚Ñù) : Icc x y = closedBall ((x + y) / 2) ((y - x) / 2) := by
  rw [Real.closedBall_eq_Icc, ‚Üê sub_div, add_comm, ‚Üê sub_add, add_sub_cancel_left, add_self_div_two,
    ‚Üê add_div, add_assoc, add_sub_cancel, add_self_div_two]

theorem Metric.uniformity_eq_comap_nhds_zero :
    ùì§ Œ± = comap (fun p : Œ± √ó Œ± => dist p.1 p.2) (ùìù (0 : ‚Ñù)) := by
  ext s
  simp only [mem_uniformity_dist, (nhds_basis_ball.comap _).mem_iff]
  simp [subset_def, Real.dist_0_eq_abs]

theorem tendsto_uniformity_iff_dist_tendsto_zero {f : Œπ ‚Üí Œ± √ó Œ±} {p : Filter Œπ} :
    Tendsto f p (ùì§ Œ±) ‚Üî Tendsto (fun x => dist (f x).1 (f x).2) p (ùìù 0) := by
  rw [Metric.uniformity_eq_comap_nhds_zero, tendsto_comap_iff, Function.comp_def]

theorem Filter.Tendsto.congr_dist {f‚ÇÅ f‚ÇÇ : Œπ ‚Üí Œ±} {p : Filter Œπ} {a : Œ±}
    (h‚ÇÅ : Tendsto f‚ÇÅ p (ùìù a)) (h : Tendsto (fun x => dist (f‚ÇÅ x) (f‚ÇÇ x)) p (ùìù 0)) :
    Tendsto f‚ÇÇ p (ùìù a) :=
  h‚ÇÅ.congr_uniformity <| tendsto_uniformity_iff_dist_tendsto_zero.2 h

alias tendsto_of_tendsto_of_dist := Filter.Tendsto.congr_dist

theorem tendsto_iff_of_dist {f‚ÇÅ f‚ÇÇ : Œπ ‚Üí Œ±} {p : Filter Œπ} {a : Œ±}
    (h : Tendsto (fun x => dist (f‚ÇÅ x) (f‚ÇÇ x)) p (ùìù 0)) : Tendsto f‚ÇÅ p (ùìù a) ‚Üî Tendsto f‚ÇÇ p (ùìù a) :=
  Uniform.tendsto_congr <| tendsto_uniformity_iff_dist_tendsto_zero.2 h

end Real

theorem PseudoMetricSpace.dist_eq_of_dist_zero (x : Œ±) {y z : Œ±} (h : dist y z = 0) :
    dist x y = dist x z :=
  dist_comm y x ‚ñ∏ dist_comm z x ‚ñ∏ sub_eq_zero.1 (abs_nonpos_iff.1 (h ‚ñ∏ abs_dist_sub_le y z x))

theorem dist_dist_dist_le_left (x y z : Œ±) : dist (dist x z) (dist y z) ‚â§ dist x y :=
  abs_dist_sub_le ..

theorem dist_dist_dist_le_right (x y z : Œ±) : dist (dist x y) (dist x z) ‚â§ dist y z := by
  simpa only [dist_comm x] using dist_dist_dist_le_left y z x

theorem dist_dist_dist_le (x y x' y' : Œ±) : dist (dist x y) (dist x' y') ‚â§ dist x x' + dist y y' :=
  (dist_triangle _ _ _).trans <|
    add_le_add (dist_dist_dist_le_left _ _ _) (dist_dist_dist_le_right _ _ _)

theorem nhds_comap_dist (a : Œ±) : ((ùìù (0 : ‚Ñù)).comap (dist ¬∑ a)) = ùìù a := by
  simp only [@nhds_eq_comap_uniformity Œ±, Metric.uniformity_eq_comap_nhds_zero, comap_comap,
    Function.comp_def, dist_comm]

theorem tendsto_iff_dist_tendsto_zero {f : Œ≤ ‚Üí Œ±} {x : Filter Œ≤} {a : Œ±} :
    Tendsto f x (ùìù a) ‚Üî Tendsto (fun b => dist (f b) a) x (ùìù 0) := by
  rw [‚Üê nhds_comap_dist a, tendsto_comap_iff, Function.comp_def]

namespace Metric

variable {x y z : Œ±} {Œµ Œµ‚ÇÅ Œµ‚ÇÇ : ‚Ñù} {s : Set Œ±}

theorem ball_subset_interior_closedBall : ball x Œµ ‚äÜ interior (closedBall x Œµ) :=
  interior_maximal ball_subset_closedBall isOpen_ball

/-- Œµ-characterization of the closure in pseudometric spaces -/
theorem mem_closure_iff {s : Set Œ±} {a : Œ±} : a ‚àà closure s ‚Üî ‚àÄ Œµ > 0, ‚àÉ b ‚àà s, dist a b < Œµ :=
  (mem_closure_iff_nhds_basis nhds_basis_ball).trans <| by simp only [mem_ball, dist_comm]

theorem mem_closure_range_iff {e : Œ≤ ‚Üí Œ±} {a : Œ±} :
    a ‚àà closure (range e) ‚Üî ‚àÄ Œµ > 0, ‚àÉ k : Œ≤, dist a (e k) < Œµ := by
  simp only [mem_closure_iff, exists_range_iff]

theorem mem_closure_range_iff_nat {e : Œ≤ ‚Üí Œ±} {a : Œ±} :
    a ‚àà closure (range e) ‚Üî ‚àÄ n : ‚Ñï, ‚àÉ k : Œ≤, dist a (e k) < 1 / ((n : ‚Ñù) + 1) :=
  (mem_closure_iff_nhds_basis nhds_basis_ball_inv_nat_succ).trans <| by
    simp only [mem_ball, dist_comm, exists_range_iff, forall_const]

theorem mem_of_closed' {s : Set Œ±} (hs : IsClosed s) {a : Œ±} :
    a ‚àà s ‚Üî ‚àÄ Œµ > 0, ‚àÉ b ‚àà s, dist a b < Œµ := by
  simpa only [hs.closure_eq] using @mem_closure_iff _ _ s a

theorem dense_iff {s : Set Œ±} : Dense s ‚Üî ‚àÄ x, ‚àÄ r > 0, (ball x r ‚à© s).Nonempty :=
  forall_congr' fun x => by
    simp only [mem_closure_iff, Set.Nonempty, mem_inter_iff, mem_ball', and_comm]

theorem dense_iff_iUnion_ball (s : Set Œ±) : Dense s ‚Üî ‚àÄ r > 0, ‚ãÉ c ‚àà s, ball c r = univ := by
  simp_rw [eq_univ_iff_forall, mem_iUnion, exists_prop, mem_ball, Dense, mem_closure_iff,
    forall_comm (Œ± := Œ±)]

theorem denseRange_iff {f : Œ≤ ‚Üí Œ±} : DenseRange f ‚Üî ‚àÄ x, ‚àÄ r > 0, ‚àÉ y, dist x (f y) < r :=
  forall_congr' fun x => by simp only [mem_closure_iff, exists_range_iff]

end Metric

open Additive Multiplicative

instance : PseudoMetricSpace (Additive Œ±) := ‚Äπ_‚Ä∫
instance : PseudoMetricSpace (Multiplicative Œ±) := ‚Äπ_‚Ä∫

section

variable [PseudoMetricSpace X]

@[simp] theorem nndist_ofMul (a b : X) : nndist (ofMul a) (ofMul b) = nndist a b := rfl

@[simp] theorem nndist_ofAdd (a b : X) : nndist (ofAdd a) (ofAdd b) = nndist a b := rfl

@[simp] theorem nndist_toMul (a b : Additive X) : nndist a.toMul b.toMul = nndist a b := rfl

@[simp]
theorem nndist_toAdd (a b : Multiplicative X) : nndist a.toAdd b.toAdd = nndist a b := rfl

end

open OrderDual

instance : PseudoMetricSpace Œ±·µí·µà := ‚Äπ_‚Ä∫

section

variable [PseudoMetricSpace X]

@[simp] theorem nndist_toDual (a b : X) : nndist (toDual a) (toDual b) = nndist a b := rfl

@[simp] theorem nndist_ofDual (a b : X·µí·µà) : nndist (ofDual a) (ofDual b) = nndist a b := rfl

end
