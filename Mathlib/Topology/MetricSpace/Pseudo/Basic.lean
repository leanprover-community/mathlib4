/-
Copyright (c) 2015, 2017 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Robert Y. Lewis, Johannes HÃ¶lzl, Mario Carneiro, SÃ©bastien GouÃ«zel
-/
import Mathlib.Data.ENNReal.Real
import Mathlib.Tactic.Bound.Attribute
import Mathlib.Topology.EMetricSpace.Basic
import Mathlib.Topology.MetricSpace.Pseudo.Defs

/-!
## Pseudo-metric spaces

Further results about pseudo-metric spaces.

-/

open Set Filter TopologicalSpace Bornology
open scoped ENNReal NNReal Uniformity Topology

universe u v

variable {Î± : Type u} {Î² : Type v} {Î¹ : Type*}

variable [PseudoMetricSpace Î±]

/-- The triangle (polygon) inequality for sequences of points; `Finset.Ico` version. -/
theorem dist_le_Ico_sum_dist (f : â„• â†’ Î±) {m n} (h : m â‰¤ n) :
    dist (f m) (f n) â‰¤ âˆ‘ i âˆˆ Finset.Ico m n, dist (f i) (f (i + 1)) := by
  induction n, h using Nat.le_induction with
  | base => rw [Finset.Ico_self, Finset.sum_empty, dist_self]
  | succ n hle ihn =>
    calc
      dist (f m) (f (n + 1)) â‰¤ dist (f m) (f n) + dist (f n) (f (n + 1)) := dist_triangle _ _ _
      _ â‰¤ (âˆ‘ i âˆˆ Finset.Ico m n, _) + _ := add_le_add ihn le_rfl
      _ = âˆ‘ i âˆˆ Finset.Ico m (n + 1), _ := by
        rw [â† Finset.insert_Ico_right_eq_Ico_add_one hle, Finset.sum_insert, add_comm]; simp

/-- The triangle (polygon) inequality for sequences of points; `Finset.range` version. -/
theorem dist_le_range_sum_dist (f : â„• â†’ Î±) (n : â„•) :
    dist (f 0) (f n) â‰¤ âˆ‘ i âˆˆ Finset.range n, dist (f i) (f (i + 1)) :=
  Nat.Ico_zero_eq_range â–¸ dist_le_Ico_sum_dist f (Nat.zero_le n)

/-- A version of `dist_le_Ico_sum_dist` with each intermediate distance replaced
with an upper estimate. -/
theorem dist_le_Ico_sum_of_dist_le {f : â„• â†’ Î±} {m n} (hmn : m â‰¤ n) {d : â„• â†’ â„}
    (hd : âˆ€ {k}, m â‰¤ k â†’ k < n â†’ dist (f k) (f (k + 1)) â‰¤ d k) :
    dist (f m) (f n) â‰¤ âˆ‘ i âˆˆ Finset.Ico m n, d i :=
  le_trans (dist_le_Ico_sum_dist f hmn) <|
    Finset.sum_le_sum fun _k hk => hd (Finset.mem_Ico.1 hk).1 (Finset.mem_Ico.1 hk).2

/-- A version of `dist_le_range_sum_dist` with each intermediate distance replaced
with an upper estimate. -/
theorem dist_le_range_sum_of_dist_le {f : â„• â†’ Î±} (n : â„•) {d : â„• â†’ â„}
    (hd : âˆ€ {k}, k < n â†’ dist (f k) (f (k + 1)) â‰¤ d k) :
    dist (f 0) (f n) â‰¤ âˆ‘ i âˆˆ Finset.range n, d i :=
  Nat.Ico_zero_eq_range â–¸ dist_le_Ico_sum_of_dist_le (zero_le n) fun _ => hd

namespace Metric

-- instantiate pseudometric space as a topology

nonrec theorem isUniformInducing_iff [PseudoMetricSpace Î²] {f : Î± â†’ Î²} :
    IsUniformInducing f â†” UniformContinuous f âˆ§
      âˆ€ Î´ > 0, âˆƒ Îµ > 0, âˆ€ {a b : Î±}, dist (f a) (f b) < Îµ â†’ dist a b < Î´ :=
  isUniformInducing_iff'.trans <| Iff.rfl.and <|
    ((uniformity_basis_dist.comap _).le_basis_iff uniformity_basis_dist).trans <| by
      simp only [subset_def, Prod.forall, gt_iff_lt, preimage_setOf_eq, Prod.map_apply, mem_setOf]

nonrec theorem isUniformEmbedding_iff [PseudoMetricSpace Î²] {f : Î± â†’ Î²} :
    IsUniformEmbedding f â†” Function.Injective f âˆ§ UniformContinuous f âˆ§
      âˆ€ Î´ > 0, âˆƒ Îµ > 0, âˆ€ {a b : Î±}, dist (f a) (f b) < Îµ â†’ dist a b < Î´ := by
  rw [isUniformEmbedding_iff, and_comm, isUniformInducing_iff]

/-- If a map between pseudometric spaces is a uniform embedding then the distance between `f x`
and `f y` is controlled in terms of the distance between `x` and `y`. -/
theorem controlled_of_isUniformEmbedding [PseudoMetricSpace Î²] {f : Î± â†’ Î²}
    (h : IsUniformEmbedding f) :
    (âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ {a b : Î±}, dist a b < Î´ â†’ dist (f a) (f b) < Îµ) âˆ§
      âˆ€ Î´ > 0, âˆƒ Îµ > 0, âˆ€ {a b : Î±}, dist (f a) (f b) < Îµ â†’ dist a b < Î´ :=
  âŸ¨uniformContinuous_iff.1 h.uniformContinuous, (isUniformEmbedding_iff.1 h).2.2âŸ©

theorem totallyBounded_iff {s : Set Î±} :
    TotallyBounded s â†” âˆ€ Îµ > 0, âˆƒ t : Set Î±, t.Finite âˆ§ s âŠ† â‹ƒ y âˆˆ t, ball y Îµ :=
  uniformity_basis_dist.totallyBounded_iff

/-- A pseudometric space is totally bounded if one can reconstruct up to any Îµ>0 any element of the
space from finitely many data. -/
theorem totallyBounded_of_finite_discretization {s : Set Î±}
    (H : âˆ€ Îµ > (0 : â„),
        âˆƒ (Î² : Type u) (_ : Fintype Î²) (F : s â†’ Î²), âˆ€ x y, F x = F y â†’ dist (x : Î±) y < Îµ) :
    TotallyBounded s := by
  rcases s.eq_empty_or_nonempty with hs | hs
  Â· rw [hs]
    exact totallyBounded_empty
  rcases hs with âŸ¨x0, hx0âŸ©
  haveI : Inhabited s := âŸ¨âŸ¨x0, hx0âŸ©âŸ©
  refine totallyBounded_iff.2 fun Îµ Îµ0 => ?_
  rcases H Îµ Îµ0 with âŸ¨Î², fÎ², F, hFâŸ©
  let Finv := Function.invFun F
  refine âŸ¨range (Subtype.val âˆ˜ Finv), finite_range _, fun x xs => ?_âŸ©
  let x' := Finv (F âŸ¨x, xsâŸ©)
  have : F x' = F âŸ¨x, xsâŸ© := Function.invFun_eq âŸ¨âŸ¨x, xsâŸ©, rflâŸ©
  simp only [Set.mem_iUnion, Set.mem_range]
  exact âŸ¨_, âŸ¨F âŸ¨x, xsâŸ©, rflâŸ©, hF _ _ this.symmâŸ©

theorem finite_approx_of_totallyBounded {s : Set Î±} (hs : TotallyBounded s) :
    âˆ€ Îµ > 0, âˆƒ t, t âŠ† s âˆ§ Set.Finite t âˆ§ s âŠ† â‹ƒ y âˆˆ t, ball y Îµ := by
  intro Îµ Îµ_pos
  rw [totallyBounded_iff_subset] at hs
  exact hs _ (dist_mem_uniformity Îµ_pos)

/-- Expressing uniform convergence using `dist` -/
theorem tendstoUniformlyOnFilter_iff {F : Î¹ â†’ Î² â†’ Î±} {f : Î² â†’ Î±} {p : Filter Î¹} {p' : Filter Î²} :
    TendstoUniformlyOnFilter F f p p' â†”
      âˆ€ Îµ > 0, âˆ€á¶  n : Î¹ Ã— Î² in p Ã—Ë¢ p', dist (f n.snd) (F n.fst n.snd) < Îµ := by
  refine âŸ¨fun H Îµ hÎµ => H _ (dist_mem_uniformity hÎµ), fun H u hu => ?_âŸ©
  rcases mem_uniformity_dist.1 hu with âŸ¨Îµ, Îµpos, hÎµâŸ©
  exact (H Îµ Îµpos).mono fun n hn => hÎµ hn

/-- Expressing locally uniform convergence on a set using `dist`. -/
theorem tendstoLocallyUniformlyOn_iff [TopologicalSpace Î²] {F : Î¹ â†’ Î² â†’ Î±} {f : Î² â†’ Î±}
    {p : Filter Î¹} {s : Set Î²} :
    TendstoLocallyUniformlyOn F f p s â†”
      âˆ€ Îµ > 0, âˆ€ x âˆˆ s, âˆƒ t âˆˆ ğ“[s] x, âˆ€á¶  n in p, âˆ€ y âˆˆ t, dist (f y) (F n y) < Îµ := by
  refine âŸ¨fun H Îµ hÎµ => H _ (dist_mem_uniformity hÎµ), fun H u hu x hx => ?_âŸ©
  rcases mem_uniformity_dist.1 hu with âŸ¨Îµ, Îµpos, hÎµâŸ©
  rcases H Îµ Îµpos x hx with âŸ¨t, ht, HtâŸ©
  exact âŸ¨t, ht, Ht.mono fun n hs x hx => hÎµ (hs x hx)âŸ©

/-- Expressing uniform convergence on a set using `dist`. -/
theorem tendstoUniformlyOn_iff {F : Î¹ â†’ Î² â†’ Î±} {f : Î² â†’ Î±} {p : Filter Î¹} {s : Set Î²} :
    TendstoUniformlyOn F f p s â†” âˆ€ Îµ > 0, âˆ€á¶  n in p, âˆ€ x âˆˆ s, dist (f x) (F n x) < Îµ := by
  refine âŸ¨fun H Îµ hÎµ => H _ (dist_mem_uniformity hÎµ), fun H u hu => ?_âŸ©
  rcases mem_uniformity_dist.1 hu with âŸ¨Îµ, Îµpos, hÎµâŸ©
  exact (H Îµ Îµpos).mono fun n hs x hx => hÎµ (hs x hx)

/-- Expressing locally uniform convergence using `dist`. -/
theorem tendstoLocallyUniformly_iff [TopologicalSpace Î²] {F : Î¹ â†’ Î² â†’ Î±} {f : Î² â†’ Î±}
    {p : Filter Î¹} :
    TendstoLocallyUniformly F f p â†”
      âˆ€ Îµ > 0, âˆ€ x : Î², âˆƒ t âˆˆ ğ“ x, âˆ€á¶  n in p, âˆ€ y âˆˆ t, dist (f y) (F n y) < Îµ := by
  simp only [â† tendstoLocallyUniformlyOn_univ, tendstoLocallyUniformlyOn_iff, nhdsWithin_univ,
    mem_univ, forall_const]

/-- Expressing uniform convergence using `dist`. -/
theorem tendstoUniformly_iff {F : Î¹ â†’ Î² â†’ Î±} {f : Î² â†’ Î±} {p : Filter Î¹} :
    TendstoUniformly F f p â†” âˆ€ Îµ > 0, âˆ€á¶  n in p, âˆ€ x, dist (f x) (F n x) < Îµ := by
  rw [â† tendstoUniformlyOn_univ, tendstoUniformlyOn_iff]
  simp

protected theorem cauchy_iff {f : Filter Î±} :
    Cauchy f â†” NeBot f âˆ§ âˆ€ Îµ > 0, âˆƒ t âˆˆ f, âˆ€ x âˆˆ t, âˆ€ y âˆˆ t, dist x y < Îµ :=
  uniformity_basis_dist.cauchy_iff

variable {s : Set Î±}

/-- Given a point `x` in a discrete subset `s` of a pseudometric space, there is an open ball
centered at `x` and intersecting `s` only at `x`. -/
theorem exists_ball_inter_eq_singleton_of_mem_discrete [DiscreteTopology s] {x : Î±} (hx : x âˆˆ s) :
    âˆƒ Îµ > 0, Metric.ball x Îµ âˆ© s = {x} :=
  nhds_basis_ball.exists_inter_eq_singleton_of_mem_discrete hx

/-- Given a point `x` in a discrete subset `s` of a pseudometric space, there is a closed ball
of positive radius centered at `x` and intersecting `s` only at `x`. -/
theorem exists_closedBall_inter_eq_singleton_of_discrete [DiscreteTopology s] {x : Î±} (hx : x âˆˆ s) :
    âˆƒ Îµ > 0, Metric.closedBall x Îµ âˆ© s = {x} :=
  nhds_basis_closedBall.exists_inter_eq_singleton_of_mem_discrete hx

end Metric

open Metric

theorem Metric.inseparable_iff_nndist {x y : Î±} : Inseparable x y â†” nndist x y = 0 := by
  rw [EMetric.inseparable_iff, edist_nndist, ENNReal.coe_eq_zero]

alias âŸ¨Inseparable.nndist_eq_zero, _âŸ© := Metric.inseparable_iff_nndist

theorem Metric.inseparable_iff {x y : Î±} : Inseparable x y â†” dist x y = 0 := by
  rw [Metric.inseparable_iff_nndist, dist_nndist, NNReal.coe_eq_zero]

alias âŸ¨Inseparable.dist_eq_zero, _âŸ© := Metric.inseparable_iff

/-- A weaker version of `tendsto_nhds_unique` for `PseudoMetricSpace`. -/
theorem tendsto_nhds_unique_dist {f : Î² â†’ Î±} {l : Filter Î²} {x y : Î±} [NeBot l]
    (ha : Tendsto f l (ğ“ x)) (hb : Tendsto f l (ğ“ y)) : dist x y = 0 :=
  (tendsto_nhds_unique_inseparable ha hb).dist_eq_zero

section Real

theorem cauchySeq_iff_tendsto_dist_atTop_0 [Nonempty Î²] [SemilatticeSup Î²] {u : Î² â†’ Î±} :
    CauchySeq u â†” Tendsto (fun n : Î² Ã— Î² => dist (u n.1) (u n.2)) atTop (ğ“ 0) := by
  rw [cauchySeq_iff_tendsto, Metric.uniformity_eq_comap_nhds_zero, tendsto_comap_iff,
    Function.comp_def]
  simp_rw [Prod.map_fst, Prod.map_snd]

end Real

namespace Topology

/-- The preimage of a separable set by an inducing map is separable. -/
protected lemma IsInducing.isSeparable_preimage {f : Î² â†’ Î±} [TopologicalSpace Î²]
    (hf : IsInducing f) {s : Set Î±} (hs : IsSeparable s) : IsSeparable (f â»Â¹' s) := by
  have : SeparableSpace s := hs.separableSpace
  have : SecondCountableTopology s := UniformSpace.secondCountable_of_separable _
  have : IsInducing ((mapsTo_preimage f s).restrict _ _ _) :=
    (hf.comp IsInducing.subtypeVal).codRestrict _
  have := this.secondCountableTopology
  exact .of_subtype _

protected theorem IsEmbedding.isSeparable_preimage {f : Î² â†’ Î±} [TopologicalSpace Î²]
    (hf : IsEmbedding f) {s : Set Î±} (hs : IsSeparable s) : IsSeparable (f â»Â¹' s) :=
  hf.isInducing.isSeparable_preimage hs

end Topology

/-- A compact set is separable. -/
theorem IsCompact.isSeparable {s : Set Î±} (hs : IsCompact s) : IsSeparable s :=
  haveI : CompactSpace s := isCompact_iff_compactSpace.mp hs
  .of_subtype s

namespace Metric

section SecondCountable

open TopologicalSpace

/-- A pseudometric space is second countable if, for every `Îµ > 0`, there is a countable set which
is `Îµ`-dense. -/
theorem secondCountable_of_almost_dense_set
    (H : âˆ€ Îµ > (0 : â„), âˆƒ s : Set Î±, s.Countable âˆ§ âˆ€ x, âˆƒ y âˆˆ s, dist x y â‰¤ Îµ) :
    SecondCountableTopology Î± := by
  refine EMetric.secondCountable_of_almost_dense_set fun Îµ Îµ0 => ?_
  rcases ENNReal.lt_iff_exists_nnreal_btwn.1 Îµ0 with âŸ¨Îµ', Îµ'0, Îµ'ÎµâŸ©
  choose s hsc y hys hyx using H Îµ' (mod_cast Îµ'0)
  refine âŸ¨s, hsc, iUnionâ‚‚_eq_univ_iff.2 fun x => âŸ¨y x, hys _, le_trans ?_ Îµ'Îµ.leâŸ©âŸ©
  exact mod_cast hyx x

end SecondCountable

end Metric

section Compact
variable {X : Type*} [PseudoMetricSpace X] {s : Set X} {Îµ : â„}

/-- Any compact set in a pseudometric space can be covered by finitely many balls of a given
positive radius -/
theorem finite_cover_balls_of_compact (hs : IsCompact s) {e : â„} (he : 0 < e) :
    âˆƒ t âŠ† s, t.Finite âˆ§ s âŠ† â‹ƒ x âˆˆ t, ball x e :=
  let âŸ¨t, hts, htâŸ© := hs.elim_nhds_subcover _ (fun x _ => ball_mem_nhds x he)
  âŸ¨t, hts, t.finite_toSet, htâŸ©

alias IsCompact.finite_cover_balls := finite_cover_balls_of_compact

/-- Any relatively compact set in a pseudometric space can be covered by finitely many balls of a
given positive radius. -/
lemma exists_finite_cover_balls_of_isCompact_closure (hs : IsCompact (closure s)) (hÎµ : 0 < Îµ) :
    âˆƒ t âŠ† s, t.Finite âˆ§ s âŠ† â‹ƒ x âˆˆ t, ball x Îµ := by
  obtain âŸ¨t, hstâŸ© := hs.elim_finite_subcover (fun x : s â†¦ ball x Îµ) (fun _ â†¦ isOpen_ball) fun x hx â†¦
    let âŸ¨y, hy, hxyâŸ© := Metric.mem_closure_iff.1 hx _ hÎµ; mem_iUnion.2 âŸ¨âŸ¨y, hyâŸ©, hxyâŸ©
  refine âŸ¨t.map âŸ¨Subtype.val, Subtype.val_injectiveâŸ©, by simp, Finset.finite_toSet _, ?_âŸ©
  simpa using subset_closure.trans hst

end Compact

/-- If a map is continuous on a separable set `s`, then the image of `s` is also separable. -/
theorem ContinuousOn.isSeparable_image [TopologicalSpace Î²] {f : Î± â†’ Î²} {s : Set Î±}
    (hf : ContinuousOn f s) (hs : IsSeparable s) : IsSeparable (f '' s) := by
  rw [image_eq_range, â† image_univ]
  exact (isSeparable_univ_iff.2 hs.separableSpace).image hf.restrict
