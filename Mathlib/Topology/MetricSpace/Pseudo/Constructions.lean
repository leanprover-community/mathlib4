/-
Copyright (c) 2015, 2017 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Robert Y. Lewis, Johannes HÃ¶lzl, Mario Carneiro, SÃ©bastien GouÃ«zel
-/
import Mathlib.Topology.Bornology.Constructions
import Mathlib.Topology.MetricSpace.Pseudo.Defs
import Mathlib.Topology.UniformSpace.UniformEmbedding

/-!
# Products of pseudometric spaces and other constructions

This file constructs the supremum distance on binary products of pseudometric spaces and provides
instances for type synonyms.
-/

open Bornology Filter Metric Set Topology
open scoped NNReal

variable {Î± Î² : Type*} [PseudoMetricSpace Î±]

/-- Pseudometric space structure pulled back by a function. -/
abbrev PseudoMetricSpace.induced {Î± Î²} (f : Î± â†’ Î²) (m : PseudoMetricSpace Î²) :
    PseudoMetricSpace Î± where
  dist x y := dist (f x) (f y)
  dist_self _ := dist_self _
  dist_comm _ _ := dist_comm _ _
  dist_triangle _ _ _ := dist_triangle _ _ _
  edist x y := edist (f x) (f y)
  edist_dist _ _ := edist_dist _ _
  toUniformSpace := UniformSpace.comap f m.toUniformSpace
  uniformity_dist := (uniformity_basis_dist.comap _).eq_biInf
  toBornology := Bornology.induced f
  cobounded_sets := Set.ext fun s â†¦ mem_comap_iff_compl.trans <| by
    simp only [â† isBounded_def, isBounded_iff, forall_mem_image, mem_setOf]

/-- Pull back a pseudometric space structure by an inducing map. This is a version of
`PseudoMetricSpace.induced` useful in case if the domain already has a `TopologicalSpace`
structure. -/
def Topology.IsInducing.comapPseudoMetricSpace {Î± Î² : Type*} [TopologicalSpace Î±]
    [m : PseudoMetricSpace Î²] {f : Î± â†’ Î²} (hf : IsInducing f) : PseudoMetricSpace Î± :=
  .replaceTopology (.induced f m) hf.eq_induced

/-- Pull back a pseudometric space structure by a uniform inducing map. This is a version of
`PseudoMetricSpace.induced` useful in case if the domain already has a `UniformSpace`
structure. -/
def IsUniformInducing.comapPseudoMetricSpace {Î± Î²} [UniformSpace Î±] [m : PseudoMetricSpace Î²]
    (f : Î± â†’ Î²) (h : IsUniformInducing f) : PseudoMetricSpace Î± :=
  .replaceUniformity (.induced f m) h.comap_uniformity.symm

instance Subtype.pseudoMetricSpace {p : Î± â†’ Prop} : PseudoMetricSpace (Subtype p) :=
  PseudoMetricSpace.induced Subtype.val â€¹_â€º

lemma Subtype.dist_eq {p : Î± â†’ Prop} (x y : Subtype p) : dist x y = dist (x : Î±) y := rfl

lemma Subtype.nndist_eq {p : Î± â†’ Prop} (x y : Subtype p) : nndist x y = nndist (x : Î±) y := rfl

namespace MulOpposite

@[to_additive]
instance instPseudoMetricSpace : PseudoMetricSpace Î±áµáµ’áµ– :=
  PseudoMetricSpace.induced MulOpposite.unop â€¹_â€º

@[to_additive (attr := simp)]
lemma dist_unop (x y : Î±áµáµ’áµ–) : dist (unop x) (unop y) = dist x y := rfl

@[to_additive (attr := simp)]
lemma dist_op (x y : Î±) : dist (op x) (op y) = dist x y := rfl

@[to_additive (attr := simp)]
lemma nndist_unop (x y : Î±áµáµ’áµ–) : nndist (unop x) (unop y) = nndist x y := rfl

@[to_additive (attr := simp)]
lemma nndist_op (x y : Î±) : nndist (op x) (op y) = nndist x y := rfl

end MulOpposite

section NNReal

instance : PseudoMetricSpace â„â‰¥0 := Subtype.pseudoMetricSpace

lemma NNReal.dist_eq (a b : â„â‰¥0) : dist a b = |(a : â„) - b| := rfl

lemma NNReal.nndist_eq (a b : â„â‰¥0) : nndist a b = max (a - b) (b - a) :=
  eq_of_forall_ge_iff fun _ â†¦ by
    simp only [max_le_iff, tsub_le_iff_right (Î± := â„â‰¥0)]
    simp only [â† NNReal.coe_le_coe, coe_nndist, dist_eq, abs_sub_le_iff,
      tsub_le_iff_right, NNReal.coe_add]

@[simp]
lemma NNReal.nndist_zero_eq_val (z : â„â‰¥0) : nndist 0 z = z := by
  simp only [NNReal.nndist_eq, max_eq_right, tsub_zero, zero_tsub, zero_le']

@[simp]
lemma NNReal.nndist_zero_eq_val' (z : â„â‰¥0) : nndist z 0 = z := by
  rw [nndist_comm]
  exact NNReal.nndist_zero_eq_val z

lemma NNReal.le_add_nndist (a b : â„â‰¥0) : a â‰¤ b + nndist a b := by
  suffices (a : â„) â‰¤ (b : â„) + dist a b by
    rwa [â† NNReal.coe_le_coe, NNReal.coe_add, coe_nndist]
  rw [â† sub_le_iff_le_add']
  exact le_of_abs_le (dist_eq a b).ge

lemma NNReal.ball_zero_eq_Ico' (c : â„â‰¥0) :
    Metric.ball (0 : â„â‰¥0) c.toReal = Set.Ico 0 c := by ext x; simp

lemma NNReal.ball_zero_eq_Ico (c : â„) :
    Metric.ball (0 : â„â‰¥0) c = Set.Ico 0 c.toNNReal := by
  by_cases c_pos : 0 < c
  Â· convert NNReal.ball_zero_eq_Ico' âŸ¨c, c_pos.leâŸ©
    simp [Real.toNNReal, c_pos.le]
  simp [not_lt.mp c_pos]

lemma NNReal.closedBall_zero_eq_Icc' (c : â„â‰¥0) :
    Metric.closedBall (0 : â„â‰¥0) c.toReal = Set.Icc 0 c := by ext x; simp

lemma NNReal.closedBall_zero_eq_Icc {c : â„} (c_nn : 0 â‰¤ c) :
    Metric.closedBall (0 : â„â‰¥0) c = Set.Icc 0 c.toNNReal := by
  convert NNReal.closedBall_zero_eq_Icc' âŸ¨c, c_nnâŸ©
  simp [Real.toNNReal, c_nn]

end NNReal

namespace ULift
variable [PseudoMetricSpace Î²]

instance : PseudoMetricSpace (ULift Î²) := PseudoMetricSpace.induced ULift.down â€¹_â€º

lemma dist_eq (x y : ULift Î²) : dist x y = dist x.down y.down := rfl

lemma nndist_eq (x y : ULift Î²) : nndist x y = nndist x.down y.down := rfl

@[simp] lemma dist_up_up (x y : Î²) : dist (ULift.up x) (ULift.up y) = dist x y := rfl

@[simp] lemma nndist_up_up (x y : Î²) : nndist (ULift.up x) (ULift.up y) = nndist x y := rfl

end ULift

section Prod
variable [PseudoMetricSpace Î²]

instance Prod.pseudoMetricSpaceMax : PseudoMetricSpace (Î± Ã— Î²) :=
  let i := PseudoEMetricSpace.toPseudoMetricSpaceOfDist
    (fun x y : Î± Ã— Î² â†¦ dist x.1 y.1 âŠ” dist x.2 y.2)
    (fun _ _ â†¦ (max_lt (edist_lt_top _ _) (edist_lt_top _ _)).ne) fun x y â†¦ by
      simp only [dist_edist, â† ENNReal.toReal_max (edist_ne_top _ _) (edist_ne_top _ _),
        Prod.edist_eq]
  i.replaceBornology fun s â†¦ by
    simp only [â† isBounded_image_fst_and_snd, isBounded_iff_eventually, forall_mem_image, â†
      eventually_and, â† forall_and, â† max_le_iff]
    rfl

lemma Prod.dist_eq {x y : Î± Ã— Î²} : dist x y = max (dist x.1 y.1) (dist x.2 y.2) := rfl

@[simp]
lemma dist_prod_same_left {x : Î±} {yâ‚ yâ‚‚ : Î²} : dist (x, yâ‚) (x, yâ‚‚) = dist yâ‚ yâ‚‚ := by
  simp [Prod.dist_eq]

@[simp]
lemma dist_prod_same_right {xâ‚ xâ‚‚ : Î±} {y : Î²} : dist (xâ‚, y) (xâ‚‚, y) = dist xâ‚ xâ‚‚ := by
  simp [Prod.dist_eq]

lemma ball_prod_same (x : Î±) (y : Î²) (r : â„) : ball x r Ã—Ë¢ ball y r = ball (x, y) r :=
  ext fun z â†¦ by simp [Prod.dist_eq]

lemma closedBall_prod_same (x : Î±) (y : Î²) (r : â„) :
    closedBall x r Ã—Ë¢ closedBall y r = closedBall (x, y) r := ext fun z â†¦ by simp [Prod.dist_eq]

lemma sphere_prod (x : Î± Ã— Î²) (r : â„) :
    sphere x r = sphere x.1 r Ã—Ë¢ closedBall x.2 r âˆª closedBall x.1 r Ã—Ë¢ sphere x.2 r := by
  obtain hr | rfl | hr := lt_trichotomy r 0
  Â· simp [hr]
  Â· cases x
    simp_rw [â† closedBall_eq_sphere_of_nonpos le_rfl, union_self, closedBall_prod_same]
  Â· ext âŸ¨x', y'âŸ©
    simp_rw [Set.mem_union, Set.mem_prod, Metric.mem_closedBall, Metric.mem_sphere, Prod.dist_eq,
      max_eq_iff]
    refine or_congr (and_congr_right ?_) (and_comm.trans (and_congr_left ?_))
    all_goals rintro rfl; rfl

end Prod

lemma uniformContinuous_dist : UniformContinuous fun p : Î± Ã— Î± â†¦ dist p.1 p.2 :=
  Metric.uniformContinuous_iff.2 fun Îµ Îµ0 â†¦
    âŸ¨Îµ / 2, half_pos Îµ0, fun {a b} h â†¦
      calc dist (dist a.1 a.2) (dist b.1 b.2) â‰¤ dist a.1 b.1 + dist a.2 b.2 :=
        dist_dist_dist_le _ _ _ _
      _ â‰¤ dist a b + dist a b := add_le_add (le_max_left _ _) (le_max_right _ _)
      _ < Îµ / 2 + Îµ / 2 := add_lt_add h h
      _ = Îµ := add_halves ÎµâŸ©

protected lemma UniformContinuous.dist [UniformSpace Î²] {f g : Î² â†’ Î±} (hf : UniformContinuous f)
    (hg : UniformContinuous g) : UniformContinuous fun b â†¦ dist (f b) (g b) :=
  uniformContinuous_dist.comp (hf.prodMk hg)

@[continuity]
lemma continuous_dist : Continuous fun p : Î± Ã— Î± â†¦ dist p.1 p.2 := uniformContinuous_dist.continuous

@[continuity, fun_prop]
protected lemma Continuous.dist [TopologicalSpace Î²] {f g : Î² â†’ Î±} (hf : Continuous f)
    (hg : Continuous g) : Continuous fun b â†¦ dist (f b) (g b) :=
  continuous_dist.compâ‚‚ hf hg

protected lemma Filter.Tendsto.dist {f g : Î² â†’ Î±} {x : Filter Î²} {a b : Î±}
    (hf : Tendsto f x (ð“ a)) (hg : Tendsto g x (ð“ b)) :
    Tendsto (fun x â†¦ dist (f x) (g x)) x (ð“ (dist a b)) :=
  (continuous_dist.tendsto (a, b)).comp (hf.prodMk_nhds hg)

lemma continuous_iff_continuous_dist [TopologicalSpace Î²] {f : Î² â†’ Î±} :
    Continuous f â†” Continuous fun x : Î² Ã— Î² â†¦ dist (f x.1) (f x.2) :=
  âŸ¨fun h â†¦ h.fst'.dist h.snd', fun h â†¦
    continuous_iff_continuousAt.2 fun _ â†¦ tendsto_iff_dist_tendsto_zero.2 <|
      (h.comp (.prodMk_left _)).tendsto' _ _ <| dist_self _âŸ©

lemma uniformContinuous_nndist : UniformContinuous fun p : Î± Ã— Î± â†¦ nndist p.1 p.2 :=
  uniformContinuous_dist.subtype_mk _

protected lemma UniformContinuous.nndist [UniformSpace Î²] {f g : Î² â†’ Î±} (hf : UniformContinuous f)
    (hg : UniformContinuous g) : UniformContinuous fun b â†¦ nndist (f b) (g b) :=
  uniformContinuous_nndist.comp (hf.prodMk hg)

lemma continuous_nndist : Continuous fun p : Î± Ã— Î± â†¦ nndist p.1 p.2 :=
  uniformContinuous_nndist.continuous

@[fun_prop]
protected lemma Continuous.nndist [TopologicalSpace Î²] {f g : Î² â†’ Î±} (hf : Continuous f)
    (hg : Continuous g) : Continuous fun b â†¦ nndist (f b) (g b) :=
  continuous_nndist.compâ‚‚ hf hg

protected lemma Filter.Tendsto.nndist {f g : Î² â†’ Î±} {x : Filter Î²} {a b : Î±}
    (hf : Tendsto f x (ð“ a)) (hg : Tendsto g x (ð“ b)) :
    Tendsto (fun x â†¦ nndist (f x) (g x)) x (ð“ (nndist a b)) :=
  (continuous_nndist.tendsto (a, b)).comp (hf.prodMk_nhds hg)
