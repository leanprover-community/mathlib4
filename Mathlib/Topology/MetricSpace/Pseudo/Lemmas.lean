/-
Copyright (c) 2015, 2017 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Robert Y. Lewis, Johannes HÃ¶lzl, Mario Carneiro, SÃ©bastien GouÃ«zel
-/
import Mathlib.Topology.MetricSpace.Pseudo.Constructions
import Mathlib.Topology.Order.DenselyOrdered
import Mathlib.Topology.UniformSpace.Compact

/-!
# Extra lemmas about pseudo-metric spaces
-/

open Bornology Filter Metric Set
open scoped NNReal Topology

variable {Î¹ Î± : Type*} [PseudoMetricSpace Î±]

instance : OrderTopology â„ :=
  orderTopology_of_nhds_abs fun x => by
    simp only [nhds_basis_ball.eq_biInf, ball, Real.dist_eq, abs_sub_comm]

lemma Real.singleton_eq_inter_Icc (b : â„) : {b} = â‹‚ (r > 0), Icc (b - r) (b + r) := by
  simp [Icc_eq_closedBall, biInter_basis_nhds Metric.nhds_basis_closedBall]

/-- Special case of the sandwich lemma; see `tendsto_of_tendsto_of_tendsto_of_le_of_le'` for the
general case. -/
lemma squeeze_zero' {Î±} {f g : Î± â†’ â„} {tâ‚€ : Filter Î±} (hf : âˆ€á¶  t in tâ‚€, 0 â‰¤ f t)
    (hft : âˆ€á¶  t in tâ‚€, f t â‰¤ g t) (g0 : Tendsto g tâ‚€ (ğ“ 0)) : Tendsto f tâ‚€ (ğ“ 0) :=
  tendsto_of_tendsto_of_tendsto_of_le_of_le' tendsto_const_nhds g0 hf hft

/-- Special case of the sandwich lemma; see `tendsto_of_tendsto_of_tendsto_of_le_of_le`
and `tendsto_of_tendsto_of_tendsto_of_le_of_le'` for the general case. -/
lemma squeeze_zero {Î±} {f g : Î± â†’ â„} {tâ‚€ : Filter Î±} (hf : âˆ€ t, 0 â‰¤ f t) (hft : âˆ€ t, f t â‰¤ g t)
    (g0 : Tendsto g tâ‚€ (ğ“ 0)) : Tendsto f tâ‚€ (ğ“ 0) :=
  squeeze_zero' (Eventually.of_forall hf) (Eventually.of_forall hft) g0

/-- If `u` is a neighborhood of `x`, then for small enough `r`, the closed ball
`Metric.closedBall x r` is contained in `u`. -/
lemma eventually_closedBall_subset {x : Î±} {u : Set Î±} (hu : u âˆˆ ğ“ x) :
    âˆ€á¶  r in ğ“ (0 : â„), closedBall x r âŠ† u := by
  obtain âŸ¨Îµ, Îµpos, hÎµâŸ© : âˆƒ Îµ, 0 < Îµ âˆ§ closedBall x Îµ âŠ† u := nhds_basis_closedBall.mem_iff.1 hu
  have : Iic Îµ âˆˆ ğ“ (0 : â„) := Iic_mem_nhds Îµpos
  filter_upwards [this] with _ hr using Subset.trans (closedBall_subset_closedBall hr) hÎµ

lemma tendsto_closedBall_smallSets (x : Î±) : Tendsto (closedBall x) (ğ“ 0) (ğ“ x).smallSets :=
  tendsto_smallSets_iff.2 fun _ â†¦ eventually_closedBall_subset

namespace Metric
variable {x y z : Î±} {Îµ Îµâ‚ Îµâ‚‚ : â„} {s : Set Î±}

lemma isClosed_closedBall : IsClosed (closedBall x Îµ) :=
  isClosed_le (continuous_id.dist continuous_const) continuous_const

lemma isClosed_sphere : IsClosed (sphere x Îµ) :=
  isClosed_eq (continuous_id.dist continuous_const) continuous_const

@[simp]
lemma closure_closedBall : closure (closedBall x Îµ) = closedBall x Îµ :=
  isClosed_closedBall.closure_eq

@[simp]
lemma closure_sphere : closure (sphere x Îµ) = sphere x Îµ :=
  isClosed_sphere.closure_eq

lemma closure_ball_subset_closedBall : closure (ball x Îµ) âŠ† closedBall x Îµ :=
  closure_minimal ball_subset_closedBall isClosed_closedBall

lemma frontier_ball_subset_sphere : frontier (ball x Îµ) âŠ† sphere x Îµ :=
  frontier_lt_subset_eq (continuous_id.dist continuous_const) continuous_const

lemma frontier_closedBall_subset_sphere : frontier (closedBall x Îµ) âŠ† sphere x Îµ :=
  frontier_le_subset_eq (continuous_id.dist continuous_const) continuous_const

lemma closedBall_zero' (x : Î±) : closedBall x 0 = closure {x} :=
  Subset.antisymm
    (fun _y hy =>
      mem_closure_iff.2 fun _Îµ Îµ0 => âŸ¨x, mem_singleton x, (mem_closedBall.1 hy).trans_lt Îµ0âŸ©)
    (closure_minimal (singleton_subset_iff.2 (dist_self x).le) isClosed_closedBall)

lemma eventually_isCompact_closedBall [WeaklyLocallyCompactSpace Î±] (x : Î±) :
    âˆ€á¶  r in ğ“ (0 : â„), IsCompact (closedBall x r) := by
  rcases exists_compact_mem_nhds x with âŸ¨s, s_compact, hsâŸ©
  filter_upwards [eventually_closedBall_subset hs] with r hr
  exact IsCompact.of_isClosed_subset s_compact isClosed_closedBall hr

lemma exists_isCompact_closedBall [WeaklyLocallyCompactSpace Î±] (x : Î±) :
    âˆƒ r, 0 < r âˆ§ IsCompact (closedBall x r) := by
  have : âˆ€á¶  r in ğ“[>] 0, IsCompact (closedBall x r) :=
    eventually_nhdsWithin_of_eventually_nhds (eventually_isCompact_closedBall x)
  simpa only [and_comm] using (this.and self_mem_nhdsWithin).exists

theorem biInter_gt_closedBall (x : Î±) (r : â„) : â‹‚ r' > r, closedBall x r' = closedBall x r := by
  ext
  simp [forall_gt_imp_ge_iff_le_of_dense]

theorem biInter_gt_ball (x : Î±) (r : â„) : â‹‚ r' > r, ball x r' = closedBall x r := by
  ext
  simp [forall_gt_iff_le]

theorem biUnion_lt_ball (x : Î±) (r : â„) : â‹ƒ r' < r, ball x r' = ball x r := by
  ext
  rw [â† not_iff_not]
  simp [forall_lt_imp_le_iff_le_of_dense]

theorem biUnion_lt_closedBall (x : Î±) (r : â„) : â‹ƒ r' < r, closedBall x r' = ball x r := by
  ext
  rw [â† not_iff_not]
  simp [forall_lt_iff_le]

end Metric

theorem lebesgue_number_lemma_of_metric {s : Set Î±} {Î¹ : Sort*} {c : Î¹ â†’ Set Î±} (hs : IsCompact s)
    (hcâ‚ : âˆ€ i, IsOpen (c i)) (hcâ‚‚ : s âŠ† â‹ƒ i, c i) : âˆƒ Î´ > 0, âˆ€ x âˆˆ s, âˆƒ i, ball x Î´ âŠ† c i := by
  simpa only [ball, UniformSpace.ball, preimage_setOf_eq, dist_comm]
    using uniformity_basis_dist.lebesgue_number_lemma hs hcâ‚ hcâ‚‚

theorem lebesgue_number_lemma_of_metric_sUnion {s : Set Î±} {c : Set (Set Î±)} (hs : IsCompact s)
    (hcâ‚ : âˆ€ t âˆˆ c, IsOpen t) (hcâ‚‚ : s âŠ† â‹ƒâ‚€ c) : âˆƒ Î´ > 0, âˆ€ x âˆˆ s, âˆƒ t âˆˆ c, ball x Î´ âŠ† t := by
  rw [sUnion_eq_iUnion] at hcâ‚‚; simpa using lebesgue_number_lemma_of_metric hs (by simpa) hcâ‚‚
