/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang, Yury Kudryashov
-/
import Mathlib.Order.UpperLower.Closure
import Mathlib.Order.UpperLower.Fibration
import Mathlib.Tactic.TFAE
import Mathlib.Topology.ContinuousOn
import Mathlib.Topology.Maps.OpenQuotient

/-!
# Inseparable points in a topological space

In this file we prove basic properties of the following notions defined elsewhere.

* `Specializes` (notation: `x â¤³ y`) : a relation saying that `ğ“ x â‰¤ ğ“ y`;

* `Inseparable`: a relation saying that two points in a topological space have the same
  neighbourhoods; equivalently, they can't be separated by an open set;

* `InseparableSetoid X`: same relation, as a `Setoid`;

* `SeparationQuotient X`: the quotient of `X` by its `InseparableSetoid`.

We also prove various basic properties of the relation `Inseparable`.

## Notations

- `x â¤³ y`: notation for `Specializes x y`;
- `x ~áµ¢ y` is used as a local notation for `Inseparable x y`;
- `ğ“ x` is the neighbourhoods filter `nhds x` of a point `x`, defined elsewhere.

## Tags

topological space, separation setoid
-/


open Set Filter Function Topology

variable {X Y Z Î± Î¹ : Type*} {A : Î¹ â†’ Type*} [TopologicalSpace X] [TopologicalSpace Y]
  [TopologicalSpace Z] [âˆ€ i, TopologicalSpace (A i)] {x y z : X} {s : Set X} {f g : X â†’ Y}

/-!
### `Specializes` relation
-/

/-- A collection of equivalent definitions of `x â¤³ y`. The public API is given by `iff` lemmas
below. -/
theorem specializes_TFAE (x y : X) :
    List.TFAE [x â¤³ y,
      pure x â‰¤ ğ“ y,
      âˆ€ s : Set X , IsOpen s â†’ y âˆˆ s â†’ x âˆˆ s,
      âˆ€ s : Set X , IsClosed s â†’ x âˆˆ s â†’ y âˆˆ s,
      y âˆˆ closure ({ x } : Set X),
      closure ({ y } : Set X) âŠ† closure { x },
      ClusterPt y (pure x)] := by
  tfae_have 1 â†’ 2 := (pure_le_nhds _).trans
  tfae_have 2 â†’ 3 := fun h s hso hy => h (hso.mem_nhds hy)
  tfae_have 3 â†’ 4 := fun h s hsc hx => of_not_not fun hy => h sá¶œ hsc.isOpen_compl hy hx
  tfae_have 4 â†’ 5 := fun h => h _ isClosed_closure (subset_closure <| mem_singleton _)
  tfae_have 6 â†” 5 := isClosed_closure.closure_subset_iff.trans singleton_subset_iff
  tfae_have 5 â†” 7 := by
    rw [mem_closure_iff_clusterPt, principal_singleton]
  tfae_have 5 â†’ 1 := by
    refine fun h => (nhds_basis_opens _).ge_iff.2 ?_
    rintro s âŸ¨hy, hoâŸ©
    rcases mem_closure_iff.1 h s ho hy with âŸ¨z, hxs, rfl : z = xâŸ©
    exact ho.mem_nhds hxs
  tfae_finish

theorem specializes_iff_nhds : x â¤³ y â†” ğ“ x â‰¤ ğ“ y :=
  Iff.rfl

theorem Specializes.not_disjoint (h : x â¤³ y) : Â¬Disjoint (ğ“ x) (ğ“ y) := fun hd â†¦
  absurd (hd.mono_right h) <| by simp [NeBot.ne']

theorem specializes_iff_pure : x â¤³ y â†” pure x â‰¤ ğ“ y :=
  (specializes_TFAE x y).out 0 1

alias âŸ¨Specializes.nhds_le_nhds, _âŸ© := specializes_iff_nhds

alias âŸ¨Specializes.pure_le_nhds, _âŸ© := specializes_iff_pure

theorem ker_nhds_eq_specializes : (ğ“ x).ker = {y | y â¤³ x} := by
  ext; simp [specializes_iff_pure, le_def]

theorem specializes_iff_forall_open : x â¤³ y â†” âˆ€ s : Set X, IsOpen s â†’ y âˆˆ s â†’ x âˆˆ s :=
  (specializes_TFAE x y).out 0 2

theorem Specializes.mem_open (h : x â¤³ y) (hs : IsOpen s) (hy : y âˆˆ s) : x âˆˆ s :=
  specializes_iff_forall_open.1 h s hs hy

theorem IsOpen.not_specializes (hs : IsOpen s) (hx : x âˆ‰ s) (hy : y âˆˆ s) : Â¬x â¤³ y := fun h =>
  hx <| h.mem_open hs hy

theorem specializes_iff_forall_closed : x â¤³ y â†” âˆ€ s : Set X, IsClosed s â†’ x âˆˆ s â†’ y âˆˆ s :=
  (specializes_TFAE x y).out 0 3

theorem Specializes.mem_closed (h : x â¤³ y) (hs : IsClosed s) (hx : x âˆˆ s) : y âˆˆ s :=
  specializes_iff_forall_closed.1 h s hs hx

theorem IsClosed.not_specializes (hs : IsClosed s) (hx : x âˆˆ s) (hy : y âˆ‰ s) : Â¬x â¤³ y := fun h =>
  hy <| h.mem_closed hs hx

theorem specializes_iff_mem_closure : x â¤³ y â†” y âˆˆ closure ({x} : Set X) :=
  (specializes_TFAE x y).out 0 4

alias âŸ¨Specializes.mem_closure, _âŸ© := specializes_iff_mem_closure

theorem specializes_iff_closure_subset : x â¤³ y â†” closure ({y} : Set X) âŠ† closure {x} :=
  (specializes_TFAE x y).out 0 5

alias âŸ¨Specializes.closure_subset, _âŸ© := specializes_iff_closure_subset

theorem specializes_iff_clusterPt : x â¤³ y â†” ClusterPt y (pure x) :=
  (specializes_TFAE x y).out 0 6

theorem Filter.HasBasis.specializes_iff {Î¹} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set X}
    (h : (ğ“ y).HasBasis p s) : x â¤³ y â†” âˆ€ i, p i â†’ x âˆˆ s i :=
  specializes_iff_pure.trans h.ge_iff

theorem specializes_rfl : x â¤³ x := le_rfl

@[refl]
theorem specializes_refl (x : X) : x â¤³ x :=
  specializes_rfl

@[trans]
theorem Specializes.trans : x â¤³ y â†’ y â¤³ z â†’ x â¤³ z :=
  le_trans

theorem specializes_of_eq (e : x = y) : x â¤³ y :=
  e â–¸ specializes_refl x

alias Specializes.of_eq := specializes_of_eq

theorem specializes_of_nhdsWithin (hâ‚ : ğ“[s] x â‰¤ ğ“[s] y) (hâ‚‚ : x âˆˆ s) : x â¤³ y :=
  specializes_iff_pure.2 <|
    calc
      pure x â‰¤ ğ“[s] x := le_inf (pure_le_nhds _) (le_principal_iff.2 hâ‚‚)
      _ â‰¤ ğ“[s] y := hâ‚
      _ â‰¤ ğ“ y := inf_le_left

theorem Specializes.map_of_continuousWithinAt {s : Set X} (h : x â¤³ y)
    (hf : ContinuousWithinAt f s y) (hx : x âˆˆ s) : f x â¤³ f y := by
  rw [specializes_iff_pure] at h âŠ¢
  calc pure (f x)
    _ = map f (pure x) := (map_pure f x).symm
    _ â‰¤ map f (ğ“ y âŠ“ ğ“Ÿ s) := map_mono (le_inf h ((pure_le_principal x).mpr hx))
    _ = map f (ğ“[s] y) := rfl
    _ â‰¤ _ := hf.tendsto

theorem Specializes.map_of_continuousOn {s : Set X} (h : x â¤³ y)
    (hf : ContinuousOn f s) (hx : x âˆˆ s) (hy : y âˆˆ s) : f x â¤³ f y :=
  h.map_of_continuousWithinAt (hf.continuousWithinAt hy) hx

theorem Specializes.map_of_continuousAt (h : x â¤³ y) (hf : ContinuousAt f y) : f x â¤³ f y :=
  h.map_of_continuousWithinAt hf.continuousWithinAt (mem_univ x)

theorem Specializes.map (h : x â¤³ y) (hf : Continuous f) : f x â¤³ f y :=
  h.map_of_continuousAt hf.continuousAt

theorem Topology.IsInducing.specializes_iff (hf : IsInducing f) : f x â¤³ f y â†” x â¤³ y := by
  simp only [specializes_iff_mem_closure, hf.closure_eq_preimage_closure_image, image_singleton,
    mem_preimage]

theorem subtype_specializes_iff {p : X â†’ Prop} (x y : Subtype p) : x â¤³ y â†” (x : X) â¤³ y :=
  IsInducing.subtypeVal.specializes_iff.symm

@[simp]
theorem specializes_prod {xâ‚ xâ‚‚ : X} {yâ‚ yâ‚‚ : Y} : (xâ‚, yâ‚) â¤³ (xâ‚‚, yâ‚‚) â†” xâ‚ â¤³ xâ‚‚ âˆ§ yâ‚ â¤³ yâ‚‚ := by
  simp only [Specializes, nhds_prod_eq, prod_le_prod]

theorem Specializes.prod {xâ‚ xâ‚‚ : X} {yâ‚ yâ‚‚ : Y} (hx : xâ‚ â¤³ xâ‚‚) (hy : yâ‚ â¤³ yâ‚‚) :
    (xâ‚, yâ‚) â¤³ (xâ‚‚, yâ‚‚) :=
  specializes_prod.2 âŸ¨hx, hyâŸ©

theorem Specializes.fst {a b : X Ã— Y} (h : a â¤³ b) : a.1 â¤³ b.1 := (specializes_prod.1 h).1
theorem Specializes.snd {a b : X Ã— Y} (h : a â¤³ b) : a.2 â¤³ b.2 := (specializes_prod.1 h).2

@[simp]
theorem specializes_pi {f g : âˆ€ i, A i} : f â¤³ g â†” âˆ€ i, f i â¤³ g i := by
  simp only [Specializes, nhds_pi, pi_le_pi]

theorem not_specializes_iff_exists_open : Â¬x â¤³ y â†” âˆƒ S : Set X, IsOpen S âˆ§ y âˆˆ S âˆ§ x âˆ‰ S := by
  rw [specializes_iff_forall_open]
  push_neg
  rfl

theorem not_specializes_iff_exists_closed : Â¬x â¤³ y â†” âˆƒ S : Set X, IsClosed S âˆ§ x âˆˆ S âˆ§ y âˆ‰ S := by
  rw [specializes_iff_forall_closed]
  push_neg
  rfl

theorem IsOpen.continuous_piecewise_of_specializes [DecidablePred (Â· âˆˆ s)] (hs : IsOpen s)
    (hf : Continuous f) (hg : Continuous g) (hspec : âˆ€ x, f x â¤³ g x) :
    Continuous (s.piecewise f g) := by
  have : âˆ€ U, IsOpen U â†’ g â»Â¹' U âŠ† f â»Â¹' U := fun U hU x hx â†¦ (hspec x).mem_open hU hx
  rw [continuous_def]
  intro U hU
  rw [piecewise_preimage, ite_eq_of_subset_right _ (this U hU)]
  exact hU.preimage hf |>.inter hs |>.union (hU.preimage hg)

theorem IsClosed.continuous_piecewise_of_specializes [DecidablePred (Â· âˆˆ s)] (hs : IsClosed s)
    (hf : Continuous f) (hg : Continuous g) (hspec : âˆ€ x, g x â¤³ f x) :
    Continuous (s.piecewise f g) := by
  simpa only [piecewise_compl] using hs.isOpen_compl.continuous_piecewise_of_specializes hg hf hspec

attribute [local instance] specializationPreorder

/-- A continuous function is monotone with respect to the specialization preorders on the domain and
the codomain. -/
theorem Continuous.specialization_monotone (hf : Continuous f) : Monotone f :=
  fun _ _ h => h.map hf

lemma closure_singleton_eq_Iic (x : X) : closure {x} = Iic x :=
  Set.ext fun _ â†¦ specializes_iff_mem_closure.symm

/-- A subset `S` of a topological space is stable under specialization
if `x âˆˆ S â†’ y âˆˆ S` for all `x â¤³ y`. -/
def StableUnderSpecialization (s : Set X) : Prop :=
  âˆ€ â¦ƒx yâ¦„, x â¤³ y â†’ x âˆˆ s â†’ y âˆˆ s

/-- A subset `S` of a topological space is stable under specialization
if `x âˆˆ S â†’ y âˆˆ S` for all `y â¤³ x`. -/
def StableUnderGeneralization (s : Set X) : Prop :=
  âˆ€ â¦ƒx yâ¦„, y â¤³ x â†’ x âˆˆ s â†’ y âˆˆ s

example {s : Set X} : StableUnderSpecialization s â†” IsLowerSet s := Iff.rfl
example {s : Set X} : StableUnderGeneralization s â†” IsUpperSet s := Iff.rfl

lemma IsClosed.stableUnderSpecialization {s : Set X} (hs : IsClosed s) :
    StableUnderSpecialization s :=
  fun _ _ e â†¦ e.mem_closed hs

lemma IsOpen.stableUnderGeneralization {s : Set X} (hs : IsOpen s) :
    StableUnderGeneralization s :=
  fun _ _ e â†¦ e.mem_open hs

@[simp]
lemma stableUnderSpecialization_compl_iff {s : Set X} :
    StableUnderSpecialization sá¶œ â†” StableUnderGeneralization s :=
  isLowerSet_compl

@[simp]
lemma stableUnderGeneralization_compl_iff {s : Set X} :
    StableUnderGeneralization sá¶œ â†” StableUnderSpecialization s :=
  isUpperSet_compl

alias âŸ¨_, StableUnderGeneralization.complâŸ© := stableUnderSpecialization_compl_iff
alias âŸ¨_, StableUnderSpecialization.complâŸ© := stableUnderGeneralization_compl_iff

lemma stableUnderSpecialization_univ : StableUnderSpecialization (univ : Set X) := isLowerSet_univ
lemma stableUnderSpecialization_empty : StableUnderSpecialization (âˆ… : Set X) := isLowerSet_empty
lemma stableUnderGeneralization_univ : StableUnderGeneralization (univ : Set X) := isUpperSet_univ
lemma stableUnderGeneralization_empty : StableUnderGeneralization (âˆ… : Set X) := isUpperSet_empty

lemma stableUnderSpecialization_sUnion (S : Set (Set X))
    (H : âˆ€ s âˆˆ S, StableUnderSpecialization s) : StableUnderSpecialization (â‹ƒâ‚€ S) :=
  isLowerSet_sUnion H

lemma stableUnderSpecialization_sInter (S : Set (Set X))
    (H : âˆ€ s âˆˆ S, StableUnderSpecialization s) : StableUnderSpecialization (â‹‚â‚€ S) :=
  isLowerSet_sInter H

lemma stableUnderGeneralization_sUnion (S : Set (Set X))
    (H : âˆ€ s âˆˆ S, StableUnderGeneralization s) : StableUnderGeneralization (â‹ƒâ‚€ S) :=
  isUpperSet_sUnion H

lemma stableUnderGeneralization_sInter (S : Set (Set X))
    (H : âˆ€ s âˆˆ S, StableUnderGeneralization s) : StableUnderGeneralization (â‹‚â‚€ S) :=
  isUpperSet_sInter H

lemma stableUnderSpecialization_iUnion {Î¹ : Sort*} (S : Î¹ â†’ Set X)
    (H : âˆ€ i, StableUnderSpecialization (S i)) : StableUnderSpecialization (â‹ƒ i, S i) :=
  isLowerSet_iUnion H

lemma stableUnderSpecialization_iInter {Î¹ : Sort*} (S : Î¹ â†’ Set X)
    (H : âˆ€ i, StableUnderSpecialization (S i)) : StableUnderSpecialization (â‹‚ i, S i) :=
  isLowerSet_iInter H

lemma stableUnderGeneralization_iUnion {Î¹ : Sort*} (S : Î¹ â†’ Set X)
    (H : âˆ€ i, StableUnderGeneralization (S i)) : StableUnderGeneralization (â‹ƒ i, S i) :=
  isUpperSet_iUnion H

lemma stableUnderGeneralization_iInter {Î¹ : Sort*} (S : Î¹ â†’ Set X)
    (H : âˆ€ i, StableUnderGeneralization (S i)) : StableUnderGeneralization (â‹‚ i, S i) :=
  isUpperSet_iInter H

lemma Union_closure_singleton_eq_iff {s : Set X} :
    (â‹ƒ x âˆˆ s, closure {x}) = s â†” StableUnderSpecialization s :=
  show _ â†” IsLowerSet s by simp only [closure_singleton_eq_Iic, â† lowerClosure_eq, coe_lowerClosure]

lemma stableUnderSpecialization_iff_Union_eq {s : Set X} :
    StableUnderSpecialization s â†” (â‹ƒ x âˆˆ s, closure {x}) = s :=
  Union_closure_singleton_eq_iff.symm

alias âŸ¨StableUnderSpecialization.Union_eq, _âŸ© := stableUnderSpecialization_iff_Union_eq

/-- A set is stable under specialization iff it is a union of closed sets. -/
lemma stableUnderSpecialization_iff_exists_sUnion_eq {s : Set X} :
    StableUnderSpecialization s â†” âˆƒ (S : Set (Set X)), (âˆ€ s âˆˆ S, IsClosed s) âˆ§ â‹ƒâ‚€ S = s := by
  refine âŸ¨fun H â†¦ âŸ¨(fun x : X â†¦ closure {x}) '' s, ?_, ?_âŸ©, fun âŸ¨S, hS, eâŸ© â†¦ e â–¸
    stableUnderSpecialization_sUnion S (fun x hx â†¦ (hS x hx).stableUnderSpecialization)âŸ©
  Â· rintro _ âŸ¨_, _, rflâŸ©; exact isClosed_closure
  Â· conv_rhs => rw [â† H.Union_eq]
    simp

/-- A set is stable under generalization iff it is an intersection of open sets. -/
lemma stableUnderGeneralization_iff_exists_sInter_eq {s : Set X} :
    StableUnderGeneralization s â†” âˆƒ (S : Set (Set X)), (âˆ€ s âˆˆ S, IsOpen s) âˆ§ â‹‚â‚€ S = s := by
  refine âŸ¨?_, fun âŸ¨S, hS, eâŸ© â†¦ e â–¸
    stableUnderGeneralization_sInter S (fun x hx â†¦ (hS x hx).stableUnderGeneralization)âŸ©
  rw [â† stableUnderSpecialization_compl_iff, stableUnderSpecialization_iff_exists_sUnion_eq]
  exact fun âŸ¨S, hâ‚, hâ‚‚âŸ© â†¦ âŸ¨(Â·á¶œ) '' S, fun s âŸ¨t, ht, eâŸ© â†¦ e â–¸ (hâ‚ t ht).isOpen_compl,
    compl_injective ((sUnion_eq_compl_sInter_compl S).symm.trans hâ‚‚)âŸ©

lemma StableUnderSpecialization.preimage {s : Set Y}
    (hs : StableUnderSpecialization s) (hf : Continuous f) :
    StableUnderSpecialization (f â»Â¹' s) :=
  IsLowerSet.preimage hs hf.specialization_monotone

lemma StableUnderGeneralization.preimage {s : Set Y}
    (hs : StableUnderGeneralization s) (hf : Continuous f) :
    StableUnderGeneralization (f â»Â¹' s) :=
  IsUpperSet.preimage hs hf.specialization_monotone

/-- A map `f` between topological spaces is specializing if specializations lifts along `f`,
i.e. for each `f x' â¤³ y` there is some `x` with `x' â¤³ x` whose image is `y`. -/
def SpecializingMap (f : X â†’ Y) : Prop :=
  Relation.Fibration (flip (Â· â¤³ Â·)) (flip (Â· â¤³ Â·)) f

/-- A map `f` between topological spaces is generalizing if generalizations lifts along `f`,
i.e. for each `y â¤³ f x'` there is some `x â¤³ x'` whose image is `y`. -/
def GeneralizingMap (f : X â†’ Y) : Prop :=
  Relation.Fibration (Â· â¤³ Â·) (Â· â¤³ Â·) f

lemma specializingMap_iff_closure_singleton_subset :
    SpecializingMap f â†” âˆ€ x, closure {f x} âŠ† f '' closure {x} := by
  simp only [SpecializingMap, Relation.Fibration, flip, specializes_iff_mem_closure]; rfl

alias âŸ¨SpecializingMap.closure_singleton_subset, _âŸ© := specializingMap_iff_closure_singleton_subset

lemma SpecializingMap.stableUnderSpecialization_image (hf : SpecializingMap f)
    {s : Set X} (hs : StableUnderSpecialization s) : StableUnderSpecialization (f '' s) :=
  IsLowerSet.image_fibration hf hs

alias StableUnderSpecialization.image := SpecializingMap.stableUnderSpecialization_image

lemma specializingMap_iff_stableUnderSpecialization_image_singleton :
    SpecializingMap f â†” âˆ€ x, StableUnderSpecialization (f '' closure {x}) := by
  simpa only [closure_singleton_eq_Iic] using Relation.fibration_iff_isLowerSet_image_Iic

lemma specializingMap_iff_stableUnderSpecialization_image :
    SpecializingMap f â†” âˆ€ s, StableUnderSpecialization s â†’ StableUnderSpecialization (f '' s) :=
  Relation.fibration_iff_isLowerSet_image

lemma specializingMap_iff_closure_singleton (hf : Continuous f) :
    SpecializingMap f â†” âˆ€ x, f '' closure {x} = closure {f x} := by
  simpa only [closure_singleton_eq_Iic] using
    Relation.fibration_iff_image_Iic hf.specialization_monotone

lemma specializingMap_iff_isClosed_image_closure_singleton (hf : Continuous f) :
    SpecializingMap f â†” âˆ€ x, IsClosed (f '' closure {x}) := by
  refine âŸ¨fun h x â†¦ ?_, fun h â†¦ specializingMap_iff_stableUnderSpecialization_image_singleton.mpr
    (fun x â†¦ (h x).stableUnderSpecialization)âŸ©
  rw [(specializingMap_iff_closure_singleton hf).mp h x]
  exact isClosed_closure

lemma SpecializingMap.comp {f : X â†’ Y} {g : Y â†’ Z}
    (hf : SpecializingMap f) (hg : SpecializingMap g) :
    SpecializingMap (g âˆ˜ f) := by
  simp only [specializingMap_iff_stableUnderSpecialization_image, Set.image_comp] at *
  exact fun s h â†¦ hg _ (hf  _ h)

lemma IsClosedMap.specializingMap (hf : IsClosedMap f) : SpecializingMap f :=
  specializingMap_iff_stableUnderSpecialization_image_singleton.mpr <|
    fun _ â†¦ (hf _ isClosed_closure).stableUnderSpecialization

lemma Topology.IsInducing.specializingMap (hf : IsInducing f)
    (h : StableUnderSpecialization (range f)) : SpecializingMap f := by
  intros x y e
  obtain âŸ¨y, rflâŸ© := h e âŸ¨x, rflâŸ©
  exact âŸ¨_, hf.specializes_iff.mp e, rflâŸ©

lemma Topology.IsInducing.generalizingMap (hf : IsInducing f)
    (h : StableUnderGeneralization (range f)) : GeneralizingMap f := by
  intros x y e
  obtain âŸ¨y, rflâŸ© := h e âŸ¨x, rflâŸ©
  exact âŸ¨_, hf.specializes_iff.mp e, rflâŸ©

lemma IsOpenEmbedding.generalizingMap (hf : IsOpenEmbedding f) : GeneralizingMap f :=
  hf.isInducing.generalizingMap hf.isOpen_range.stableUnderGeneralization

lemma SpecializingMap.stableUnderSpecialization_range (h : SpecializingMap f) :
    StableUnderSpecialization (range f) :=
  @image_univ _ _ f â–¸ stableUnderSpecialization_univ.image h

lemma GeneralizingMap.stableUnderGeneralization_image (hf : GeneralizingMap f) {s : Set X}
    (hs : StableUnderGeneralization s) : StableUnderGeneralization (f '' s) :=
  IsUpperSet.image_fibration hf hs

lemma GeneralizingMap_iff_stableUnderGeneralization_image :
    GeneralizingMap f â†” âˆ€ s, StableUnderGeneralization s â†’ StableUnderGeneralization (f '' s) :=
  Relation.fibration_iff_isUpperSet_image

alias StableUnderGeneralization.image := GeneralizingMap.stableUnderGeneralization_image

lemma GeneralizingMap.stableUnderGeneralization_range (h : GeneralizingMap f) :
    StableUnderGeneralization (range f) :=
  @image_univ _ _ f â–¸ stableUnderGeneralization_univ.image h

lemma GeneralizingMap.comp {f : X â†’ Y} {g : Y â†’ Z}
    (hf : GeneralizingMap f) (hg : GeneralizingMap g) :
    GeneralizingMap (g âˆ˜ f) := by
  simp only [GeneralizingMap_iff_stableUnderGeneralization_image, Set.image_comp] at *
  exact fun s h â†¦ hg _ (hf  _ h)

/-!
### `Inseparable` relation
-/

local infixl:0 " ~áµ¢ " => Inseparable

theorem inseparable_def : (x ~áµ¢ y) â†” ğ“ x = ğ“ y :=
  Iff.rfl

theorem inseparable_iff_specializes_and : (x ~áµ¢ y) â†” x â¤³ y âˆ§ y â¤³ x :=
  le_antisymm_iff

theorem Inseparable.specializes (h : x ~áµ¢ y) : x â¤³ y := h.le

theorem Inseparable.specializes' (h : x ~áµ¢ y) : y â¤³ x := h.ge

theorem Specializes.antisymm (hâ‚ : x â¤³ y) (hâ‚‚ : y â¤³ x) : x ~áµ¢ y :=
  le_antisymm hâ‚ hâ‚‚

theorem inseparable_iff_forall_isOpen : (x ~áµ¢ y) â†” âˆ€ s : Set X, IsOpen s â†’ (x âˆˆ s â†” y âˆˆ s) := by
  simp only [inseparable_iff_specializes_and, specializes_iff_forall_open, â† forall_and, â† iff_def,
    Iff.comm]

theorem not_inseparable_iff_exists_open :
    Â¬(x ~áµ¢ y) â†” âˆƒ s : Set X, IsOpen s âˆ§ Xor' (x âˆˆ s) (y âˆˆ s) := by
  simp [inseparable_iff_forall_isOpen, â† xor_iff_not_iff]

theorem inseparable_iff_forall_isClosed : (x ~áµ¢ y) â†” âˆ€ s : Set X, IsClosed s â†’ (x âˆˆ s â†” y âˆˆ s) := by
  simp only [inseparable_iff_specializes_and, specializes_iff_forall_closed, â† forall_and, â†
    iff_def]

theorem inseparable_iff_mem_closure :
    (x ~áµ¢ y) â†” x âˆˆ closure ({y} : Set X) âˆ§ y âˆˆ closure ({x} : Set X) :=
  inseparable_iff_specializes_and.trans <| by simp only [specializes_iff_mem_closure, and_comm]

theorem inseparable_iff_closure_eq : (x ~áµ¢ y) â†” closure ({x} : Set X) = closure {y} := by
  simp only [inseparable_iff_specializes_and, specializes_iff_closure_subset, â† subset_antisymm_iff,
    eq_comm]

theorem inseparable_of_nhdsWithin_eq (hx : x âˆˆ s) (hy : y âˆˆ s) (h : ğ“[s] x = ğ“[s] y) : x ~áµ¢ y :=
  (specializes_of_nhdsWithin h.le hx).antisymm (specializes_of_nhdsWithin h.ge hy)

theorem Topology.IsInducing.inseparable_iff (hf : IsInducing f) : (f x ~áµ¢ f y) â†” (x ~áµ¢ y) := by
  simp only [inseparable_iff_specializes_and, hf.specializes_iff]

theorem subtype_inseparable_iff {p : X â†’ Prop} (x y : Subtype p) : (x ~áµ¢ y) â†” ((x : X) ~áµ¢ y) :=
  IsInducing.subtypeVal.inseparable_iff.symm

@[simp] theorem inseparable_prod {xâ‚ xâ‚‚ : X} {yâ‚ yâ‚‚ : Y} :
    ((xâ‚, yâ‚) ~áµ¢ (xâ‚‚, yâ‚‚)) â†” (xâ‚ ~áµ¢ xâ‚‚) âˆ§ (yâ‚ ~áµ¢ yâ‚‚) := by
  simp only [Inseparable, nhds_prod_eq, prod_inj]

theorem Inseparable.prod {xâ‚ xâ‚‚ : X} {yâ‚ yâ‚‚ : Y} (hx : xâ‚ ~áµ¢ xâ‚‚) (hy : yâ‚ ~áµ¢ yâ‚‚) :
    (xâ‚, yâ‚) ~áµ¢ (xâ‚‚, yâ‚‚) :=
  inseparable_prod.2 âŸ¨hx, hyâŸ©

@[simp]
theorem inseparable_pi {f g : âˆ€ i, A i} : (f ~áµ¢ g) â†” âˆ€ i, f i ~áµ¢ g i := by
  simp only [Inseparable, nhds_pi, funext_iff, pi_inj]

namespace Inseparable

@[refl]
theorem refl (x : X) : x ~áµ¢ x :=
  Eq.refl (ğ“ x)

theorem rfl : x ~áµ¢ x :=
  refl x

theorem of_eq (e : x = y) : Inseparable x y :=
  e â–¸ refl x

@[symm]
nonrec theorem symm (h : x ~áµ¢ y) : y ~áµ¢ x := h.symm

@[trans]
nonrec theorem trans (hâ‚ : x ~áµ¢ y) (hâ‚‚ : y ~áµ¢ z) : x ~áµ¢ z := hâ‚.trans hâ‚‚

theorem nhds_eq (h : x ~áµ¢ y) : ğ“ x = ğ“ y := h

theorem mem_open_iff (h : x ~áµ¢ y) (hs : IsOpen s) : x âˆˆ s â†” y âˆˆ s :=
  inseparable_iff_forall_isOpen.1 h s hs

theorem mem_closed_iff (h : x ~áµ¢ y) (hs : IsClosed s) : x âˆˆ s â†” y âˆˆ s :=
  inseparable_iff_forall_isClosed.1 h s hs

theorem map_of_continuousWithinAt {s t : Set X} (h : x ~áµ¢ y)
    (hfx : ContinuousWithinAt f s x) (hfy : ContinuousWithinAt f t y)
    (hx : x âˆˆ t) (hy : y âˆˆ s) : f x ~áµ¢ f y :=
  (h.specializes.map_of_continuousWithinAt hfy hx).antisymm
    (h.specializes'.map_of_continuousWithinAt hfx hy)

theorem map_of_continuousOn {s : Set X} (h : x ~áµ¢ y)
    (hf : ContinuousOn f s) (hx : x âˆˆ s) (hy : y âˆˆ s) : f x ~áµ¢ f y :=
  h.map_of_continuousWithinAt (hf.continuousWithinAt hx) (hf.continuousWithinAt hy) hx hy

theorem map_of_continuousAt (h : x ~áµ¢ y) (hx : ContinuousAt f x) (hy : ContinuousAt f y) :
    f x ~áµ¢ f y :=
  h.map_of_continuousWithinAt hx.continuousWithinAt hy.continuousWithinAt (mem_univ x) (mem_univ y)

theorem map (h : x ~áµ¢ y) (hf : Continuous f) : f x ~áµ¢ f y :=
  h.map_of_continuousAt hf.continuousAt hf.continuousAt

end Inseparable

theorem IsClosed.not_inseparable (hs : IsClosed s) (hx : x âˆˆ s) (hy : y âˆ‰ s) : Â¬(x ~áµ¢ y) := fun h =>
  hy <| (h.mem_closed_iff hs).1 hx

theorem IsOpen.not_inseparable (hs : IsOpen s) (hx : x âˆˆ s) (hy : y âˆ‰ s) : Â¬(x ~áµ¢ y) := fun h =>
  hy <| (h.mem_open_iff hs).1 hx

/-!
### Separation quotient

In this section we define the quotient of a topological space by the `Inseparable` relation.
-/

variable (X) in
instance : TopologicalSpace (SeparationQuotient X) := instTopologicalSpaceQuotient

variable {t : Set (SeparationQuotient X)}

namespace SeparationQuotient

/-- The natural map from a topological space to its separation quotient. -/
def mk : X â†’ SeparationQuotient X := Quotient.mk''

theorem isQuotientMap_mk : IsQuotientMap (mk : X â†’ SeparationQuotient X) :=
  isQuotientMap_quot_mk

@[fun_prop, continuity]
theorem continuous_mk : Continuous (mk : X â†’ SeparationQuotient X) :=
  continuous_quot_mk

@[simp]
theorem mk_eq_mk : mk x = mk y â†” (x ~áµ¢ y) :=
  Quotient.eq''

theorem surjective_mk : Surjective (mk : X â†’ SeparationQuotient X) :=
  Quot.mk_surjective

@[simp]
theorem range_mk : range (mk : X â†’ SeparationQuotient X) = univ :=
  surjective_mk.range_eq

instance [Nonempty X] : Nonempty (SeparationQuotient X) :=
  Nonempty.map mk â€¹_â€º

instance [Inhabited X] : Inhabited (SeparationQuotient X) :=
  âŸ¨mk defaultâŸ©

instance [Subsingleton X] : Subsingleton (SeparationQuotient X) :=
  surjective_mk.subsingleton

@[to_additive] instance [One X] : One (SeparationQuotient X) := âŸ¨mk 1âŸ©

@[to_additive (attr := simp)] theorem mk_one [One X] : mk (1 : X) = 1 := rfl

theorem preimage_image_mk_open (hs : IsOpen s) : mk â»Â¹' (mk '' s) = s := by
  refine Subset.antisymm ?_ (subset_preimage_image _ _)
  rintro x âŸ¨y, hys, hxyâŸ©
  exact ((mk_eq_mk.1 hxy).mem_open_iff hs).1 hys

theorem isOpenMap_mk : IsOpenMap (mk : X â†’ SeparationQuotient X) := fun s hs =>
  isQuotientMap_mk.isOpen_preimage.1 <| by rwa [preimage_image_mk_open hs]

theorem isOpenQuotientMap_mk : IsOpenQuotientMap (mk : X â†’ SeparationQuotient X) :=
  âŸ¨surjective_mk, continuous_mk, isOpenMap_mkâŸ©

theorem preimage_image_mk_closed (hs : IsClosed s) : mk â»Â¹' (mk '' s) = s := by
  refine Subset.antisymm ?_ (subset_preimage_image _ _)
  rintro x âŸ¨y, hys, hxyâŸ©
  exact ((mk_eq_mk.1 hxy).mem_closed_iff hs).1 hys

theorem isInducing_mk : IsInducing (mk : X â†’ SeparationQuotient X) :=
  âŸ¨le_antisymm (continuous_iff_le_induced.1 continuous_mk) fun s hs =>
      âŸ¨mk '' s, isOpenMap_mk s hs, preimage_image_mk_open hsâŸ©âŸ©

theorem isClosedMap_mk : IsClosedMap (mk : X â†’ SeparationQuotient X) :=
  isInducing_mk.isClosedMap <| by rw [range_mk]; exact isClosed_univ

@[simp]
theorem comap_mk_nhds_mk : comap mk (ğ“ (mk x)) = ğ“ x :=
  (isInducing_mk.nhds_eq_comap _).symm

@[simp]
theorem comap_mk_nhdsSet_image : comap mk (ğ“Ë¢ (mk '' s)) = ğ“Ë¢ s :=
  (isInducing_mk.nhdsSet_eq_comap _).symm

/-- Push-forward of the neighborhood of a point along the projection to the separation quotient
is the neighborhood of its equivalence class. -/
theorem map_mk_nhds : map mk (ğ“ x) = ğ“ (mk x) := by
  rw [â† comap_mk_nhds_mk, map_comap_of_surjective surjective_mk]

@[deprecated map_mk_nhds (since := "2025-03-21")]
theorem nhds_mk (x : X) : ğ“ (mk x) = .map mk (ğ“ x) := .symm <| map_mk_nhds ..

theorem map_mk_nhdsSet : map mk (ğ“Ë¢ s) = ğ“Ë¢ (mk '' s) := by
  rw [â† comap_mk_nhdsSet_image, map_comap_of_surjective surjective_mk]

theorem comap_mk_nhdsSet : comap mk (ğ“Ë¢ t) = ğ“Ë¢ (mk â»Â¹' t) := by
  conv_lhs => rw [â† image_preimage_eq t surjective_mk, comap_mk_nhdsSet_image]

theorem preimage_mk_closure : mk â»Â¹' closure t = closure (mk â»Â¹' t) :=
  isOpenMap_mk.preimage_closure_eq_closure_preimage continuous_mk t

theorem preimage_mk_interior : mk â»Â¹' interior t = interior (mk â»Â¹' t) :=
  isOpenMap_mk.preimage_interior_eq_interior_preimage continuous_mk t

theorem preimage_mk_frontier : mk â»Â¹' frontier t = frontier (mk â»Â¹' t) :=
  isOpenMap_mk.preimage_frontier_eq_frontier_preimage continuous_mk t

theorem image_mk_closure : mk '' closure s = closure (mk '' s) :=
  (image_closure_subset_closure_image continuous_mk).antisymm <|
    isClosedMap_mk.closure_image_subset _

theorem map_prod_map_mk_nhds (x : X) (y : Y) :
    map (Prod.map mk mk) (ğ“ (x, y)) = ğ“ (mk x, mk y) := by
  rw [nhds_prod_eq, â† prod_map_map_eq', map_mk_nhds, map_mk_nhds, nhds_prod_eq]

theorem map_mk_nhdsWithin_preimage (s : Set (SeparationQuotient X)) (x : X) :
    map mk (ğ“[mk â»Â¹' s] x) = ğ“[s] mk x := by
  rw [nhdsWithin, â† comap_principal, Filter.push_pull, nhdsWithin, map_mk_nhds]

/-- The map `(x, y) â†¦ (mk x, mk y)` is a quotient map. -/
theorem isQuotientMap_prodMap_mk : IsQuotientMap (Prod.map mk mk : X Ã— Y â†’ _) :=
  (isOpenQuotientMap_mk.prodMap isOpenQuotientMap_mk).isQuotientMap

/-- Lift a map `f : X â†’ Î±` such that `Inseparable x y â†’ f x = f y` to a map
`SeparationQuotient X â†’ Î±`. -/
def lift (f : X â†’ Î±) (hf : âˆ€ x y, (x ~áµ¢ y) â†’ f x = f y) : SeparationQuotient X â†’ Î± := fun x =>
  Quotient.liftOn' x f hf

@[simp]
theorem lift_mk {f : X â†’ Î±} (hf : âˆ€ x y, (x ~áµ¢ y) â†’ f x = f y) (x : X) : lift f hf (mk x) = f x :=
  rfl

@[simp]
theorem lift_comp_mk {f : X â†’ Î±} (hf : âˆ€ x y, (x ~áµ¢ y) â†’ f x = f y) : lift f hf âˆ˜ mk = f :=
  rfl

@[simp]
theorem tendsto_lift_nhds_mk {f : X â†’ Î±} {hf : âˆ€ x y, (x ~áµ¢ y) â†’ f x = f y} {l : Filter Î±} :
    Tendsto (lift f hf) (ğ“ <| mk x) l â†” Tendsto f (ğ“ x) l := by
  simp only [â† map_mk_nhds, tendsto_map'_iff, lift_comp_mk]

@[simp]
theorem tendsto_lift_nhdsWithin_mk {f : X â†’ Î±} {hf : âˆ€ x y, (x ~áµ¢ y) â†’ f x = f y}
    {s : Set (SeparationQuotient X)} {l : Filter Î±} :
    Tendsto (lift f hf) (ğ“[s] mk x) l â†” Tendsto f (ğ“[mk â»Â¹' s] x) l := by
  simp only [â† map_mk_nhdsWithin_preimage, tendsto_map'_iff, lift_comp_mk]

@[simp]
theorem continuousAt_lift {hf : âˆ€ x y, (x ~áµ¢ y) â†’ f x = f y} :
    ContinuousAt (lift f hf) (mk x) â†” ContinuousAt f x :=
  tendsto_lift_nhds_mk

@[simp]
theorem continuousWithinAt_lift {hf : âˆ€ x y, (x ~áµ¢ y) â†’ f x = f y}
    {s : Set (SeparationQuotient X)} :
    ContinuousWithinAt (lift f hf) s (mk x) â†” ContinuousWithinAt f (mk â»Â¹' s) x :=
  tendsto_lift_nhdsWithin_mk

@[simp]
theorem continuousOn_lift {hf : âˆ€ x y, (x ~áµ¢ y) â†’ f x = f y} {s : Set (SeparationQuotient X)} :
    ContinuousOn (lift f hf) s â†” ContinuousOn f (mk â»Â¹' s) := by
  simp only [ContinuousOn, surjective_mk.forall, continuousWithinAt_lift, mem_preimage]

@[simp]
theorem continuous_lift {hf : âˆ€ x y, (x ~áµ¢ y) â†’ f x = f y} :
    Continuous (lift f hf) â†” Continuous f := by
  simp only [â† continuousOn_univ, continuousOn_lift, preimage_univ]

/-- Lift a map `f : X â†’ Y â†’ Î±` such that `Inseparable a b â†’ Inseparable c d â†’ f a c = f b d` to a
map `SeparationQuotient X â†’ SeparationQuotient Y â†’ Î±`. -/
def liftâ‚‚ (f : X â†’ Y â†’ Î±) (hf : âˆ€ a b c d, (a ~áµ¢ c) â†’ (b ~áµ¢ d) â†’ f a b = f c d) :
    SeparationQuotient X â†’ SeparationQuotient Y â†’ Î± := fun x y => Quotient.liftOnâ‚‚' x y f hf

@[simp]
theorem liftâ‚‚_mk {f : X â†’ Y â†’ Î±} (hf : âˆ€ a b c d, (a ~áµ¢ c) â†’ (b ~áµ¢ d) â†’ f a b = f c d) (x : X)
    (y : Y) : liftâ‚‚ f hf (mk x) (mk y) = f x y :=
  rfl

@[simp]
theorem tendsto_liftâ‚‚_nhds {f : X â†’ Y â†’ Î±} {hf : âˆ€ a b c d, (a ~áµ¢ c) â†’ (b ~áµ¢ d) â†’ f a b = f c d}
    {x : X} {y : Y} {l : Filter Î±} :
    Tendsto (uncurry <| liftâ‚‚ f hf) (ğ“ (mk x, mk y)) l â†” Tendsto (uncurry f) (ğ“ (x, y)) l := by
  rw [â† map_prod_map_mk_nhds, tendsto_map'_iff]
  rfl

@[simp] theorem tendsto_liftâ‚‚_nhdsWithin {f : X â†’ Y â†’ Î±}
    {hf : âˆ€ a b c d, (a ~áµ¢ c) â†’ (b ~áµ¢ d) â†’ f a b = f c d} {x : X} {y : Y}
    {s : Set (SeparationQuotient X Ã— SeparationQuotient Y)} {l : Filter Î±} :
    Tendsto (uncurry <| liftâ‚‚ f hf) (ğ“[s] (mk x, mk y)) l â†”
      Tendsto (uncurry f) (ğ“[Prod.map mk mk â»Â¹' s] (x, y)) l := by
  rw [nhdsWithin, â† map_prod_map_mk_nhds, â† Filter.push_pull, comap_principal]
  rfl

@[simp]
theorem continuousAt_liftâ‚‚ {f : X â†’ Y â†’ Z} {hf : âˆ€ a b c d, (a ~áµ¢ c) â†’ (b ~áµ¢ d) â†’ f a b = f c d}
    {x : X} {y : Y} :
    ContinuousAt (uncurry <| liftâ‚‚ f hf) (mk x, mk y) â†” ContinuousAt (uncurry f) (x, y) :=
  tendsto_liftâ‚‚_nhds

@[simp] theorem continuousWithinAt_liftâ‚‚ {f : X â†’ Y â†’ Z}
    {hf : âˆ€ a b c d, (a ~áµ¢ c) â†’ (b ~áµ¢ d) â†’ f a b = f c d}
    {s : Set (SeparationQuotient X Ã— SeparationQuotient Y)} {x : X} {y : Y} :
    ContinuousWithinAt (uncurry <| liftâ‚‚ f hf) s (mk x, mk y) â†”
      ContinuousWithinAt (uncurry f) (Prod.map mk mk â»Â¹' s) (x, y) :=
  tendsto_liftâ‚‚_nhdsWithin

@[simp]
theorem continuousOn_liftâ‚‚ {f : X â†’ Y â†’ Z} {hf : âˆ€ a b c d, (a ~áµ¢ c) â†’ (b ~áµ¢ d) â†’ f a b = f c d}
    {s : Set (SeparationQuotient X Ã— SeparationQuotient Y)} :
    ContinuousOn (uncurry <| liftâ‚‚ f hf) s â†” ContinuousOn (uncurry f) (Prod.map mk mk â»Â¹' s) := by
  simp_rw [ContinuousOn, (surjective_mk.prodMap surjective_mk).forall, Prod.forall, Prod.map,
    continuousWithinAt_liftâ‚‚]
  rfl

@[simp]
theorem continuous_liftâ‚‚ {f : X â†’ Y â†’ Z} {hf : âˆ€ a b c d, (a ~áµ¢ c) â†’ (b ~áµ¢ d) â†’ f a b = f c d} :
    Continuous (uncurry <| liftâ‚‚ f hf) â†” Continuous (uncurry f) := by
  simp only [â† continuousOn_univ, continuousOn_liftâ‚‚, preimage_univ]

end SeparationQuotient

theorem continuous_congr_of_inseparable (h : âˆ€ x, f x ~áµ¢ g x) :
    Continuous f â†” Continuous g := by
  simp_rw [SeparationQuotient.isInducing_mk.continuous_iff (Y := Y)]
  exact continuous_congr fun x â†¦ SeparationQuotient.mk_eq_mk.mpr (h x)
