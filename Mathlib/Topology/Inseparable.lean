/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang, Yury G. Kudryashov
-/
import Mathlib.Tactic.TFAE
import Mathlib.Topology.ContinuousOn

#align_import topology.inseparable from "leanprover-community/mathlib"@"bcfa726826abd57587355b4b5b7e78ad6527b7e4"

/-!
# Inseparable points in a topological space

In this file we define

* `Specializes` (notation: `x â¤³ y`) : a relation saying that `ğ“ x â‰¤ ğ“ y`;

* `Inseparable`: a relation saying that two points in a topological space have the same
  neighbourhoods; equivalently, they can't be separated by an open set;

* `InseparableSetoid X`: same relation, as a `Setoid`;

* `SeparationQuotient X`: the quotient of `X` by its `InseparableSetoid`.

We also prove various basic properties of the relation `Inseparable`.

## Notations

- `x â¤³ y`: notation for `Specializes x y`;
- `x ~áµ¢ y` is used as a local notation for `Inseparable x y`;
- `ğ“ x` is the neighbourhoods filter `nhds x` of a point `x`, defined elsewhere.

## Tags

topological space, separation setoid
-/


open Set Filter Function Topology List

variable {X Y Z Î± Î¹ : Type*} {Ï€ : Î¹ â†’ Type*} [TopologicalSpace X] [TopologicalSpace Y]
  [TopologicalSpace Z] [âˆ€ i, TopologicalSpace (Ï€ i)] {x y z : X} {s : Set X} {f g : X â†’ Y}

/-!
### `Specializes` relation
-/

/-- `x` specializes to `y` (notation: `x â¤³ y`) if either of the following equivalent properties
hold:

* `ğ“ x â‰¤ ğ“ y`; this property is used as the definition;
* `pure x â‰¤ ğ“ y`; in other words, any neighbourhood of `y` contains `x`;
* `y âˆˆ closure {x}`;
* `closure {y} âŠ† closure {x}`;
* for any closed set `s` we have `x âˆˆ s â†’ y âˆˆ s`;
* for any open set `s` we have `y âˆˆ s â†’ x âˆˆ s`;
* `y` is a cluster point of the filter `pure x = ğ“Ÿ {x}`.

This relation defines a `Preorder` on `X`. If `X` is a Tâ‚€ space, then this preorder is a partial
order. If `X` is a Tâ‚ space, then this partial order is trivial : `x â¤³ y â†” x = y`. -/
def Specializes (x y : X) : Prop := ğ“ x â‰¤ ğ“ y

@[inherit_doc]
infixl:300 " â¤³ " => Specializes

/-- A collection of equivalent definitions of `x â¤³ y`. The public API is given by `iff` lemmas
below. -/
theorem specializes_TFAE (x y : X) :
    TFAE [x â¤³ y,
      pure x â‰¤ ğ“ y,
      âˆ€ s : Set X , IsOpen s â†’ y âˆˆ s â†’ x âˆˆ s,
      âˆ€ s : Set X , IsClosed s â†’ x âˆˆ s â†’ y âˆˆ s,
      y âˆˆ closure ({ x } : Set X),
      closure ({ y } : Set X) âŠ† closure { x },
      ClusterPt y (pure x)] := by
  tfae_have 1 â†’ 2
  Â· exact (pure_le_nhds _).trans
  tfae_have 2 â†’ 3
  Â· exact fun h s hso hy => h (hso.mem_nhds hy)
  tfae_have 3 â†’ 4
  Â· exact fun h s hsc hx => of_not_not fun hy => h sá¶œ hsc.isOpen_compl hy hx
  tfae_have 4 â†’ 5
  Â· exact fun h => h _ isClosed_closure (subset_closure <| mem_singleton _)
  tfae_have 6 â†” 5
  Â· exact isClosed_closure.closure_subset_iff.trans singleton_subset_iff
  tfae_have 5 â†” 7
  Â· rw [mem_closure_iff_clusterPt, principal_singleton]
  tfae_have 5 â†’ 1
  Â· refine' fun h => (nhds_basis_opens _).ge_iff.2 _
    rintro s âŸ¨hy, hoâŸ©
    rcases mem_closure_iff.1 h s ho hy with âŸ¨z, hxs, rfl : z = xâŸ©
    exact ho.mem_nhds hxs
  tfae_finish

theorem specializes_iff_nhds : x â¤³ y â†” ğ“ x â‰¤ ğ“ y :=
  Iff.rfl

theorem specializes_iff_pure : x â¤³ y â†” pure x â‰¤ ğ“ y :=
  (specializes_TFAE x y).out 0 1

alias âŸ¨Specializes.nhds_le_nhds, _âŸ© := specializes_iff_nhds

alias âŸ¨Specializes.pure_le_nhds, _âŸ© := specializes_iff_pure

theorem ker_nhds_eq_specializes : (ğ“ x).ker = {y | y â¤³ x} := by
  ext; simp [specializes_iff_pure, le_def]

theorem specializes_iff_forall_open : x â¤³ y â†” âˆ€ s : Set X, IsOpen s â†’ y âˆˆ s â†’ x âˆˆ s :=
  (specializes_TFAE x y).out 0 2

theorem Specializes.mem_open (h : x â¤³ y) (hs : IsOpen s) (hy : y âˆˆ s) : x âˆˆ s :=
  specializes_iff_forall_open.1 h s hs hy

theorem IsOpen.not_specializes (hs : IsOpen s) (hx : x âˆ‰ s) (hy : y âˆˆ s) : Â¬x â¤³ y := fun h =>
  hx <| h.mem_open hs hy

theorem specializes_iff_forall_closed : x â¤³ y â†” âˆ€ s : Set X, IsClosed s â†’ x âˆˆ s â†’ y âˆˆ s :=
  (specializes_TFAE x y).out 0 3

theorem Specializes.mem_closed (h : x â¤³ y) (hs : IsClosed s) (hx : x âˆˆ s) : y âˆˆ s :=
  specializes_iff_forall_closed.1 h s hs hx

theorem IsClosed.not_specializes (hs : IsClosed s) (hx : x âˆˆ s) (hy : y âˆ‰ s) : Â¬x â¤³ y := fun h =>
  hy <| h.mem_closed hs hx

theorem specializes_iff_mem_closure : x â¤³ y â†” y âˆˆ closure ({x} : Set X) :=
  (specializes_TFAE x y).out 0 4

alias âŸ¨Specializes.mem_closure, _âŸ© := specializes_iff_mem_closure

theorem specializes_iff_closure_subset : x â¤³ y â†” closure ({y} : Set X) âŠ† closure {x} :=
  (specializes_TFAE x y).out 0 5

alias âŸ¨Specializes.closure_subset, _âŸ© := specializes_iff_closure_subset

-- porting note: new lemma
theorem specializes_iff_clusterPt : x â¤³ y â†” ClusterPt y (pure x) :=
  (specializes_TFAE x y).out 0 6

theorem Filter.HasBasis.specializes_iff {Î¹} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set X}
    (h : (ğ“ y).HasBasis p s) : x â¤³ y â†” âˆ€ i, p i â†’ x âˆˆ s i :=
  specializes_iff_pure.trans h.ge_iff

theorem specializes_rfl : x â¤³ x := le_rfl

@[refl]
theorem specializes_refl (x : X) : x â¤³ x :=
  specializes_rfl

@[trans]
theorem Specializes.trans : x â¤³ y â†’ y â¤³ z â†’ x â¤³ z :=
  le_trans

theorem specializes_of_eq (e : x = y) : x â¤³ y :=
  e â–¸ specializes_refl x

theorem specializes_of_nhdsWithin (hâ‚ : ğ“[s] x â‰¤ ğ“[s] y) (hâ‚‚ : x âˆˆ s) : x â¤³ y :=
  specializes_iff_pure.2 <|
    calc
      pure x â‰¤ ğ“[s] x := le_inf (pure_le_nhds _) (le_principal_iff.2 hâ‚‚)
      _ â‰¤ ğ“[s] y := hâ‚
      _ â‰¤ ğ“ y := inf_le_left

theorem Specializes.map_of_continuousAt (h : x â¤³ y) (hy : ContinuousAt f y) : f x â¤³ f y :=
  specializes_iff_pure.2 fun _s hs =>
    mem_pure.2 <| mem_preimage.1 <| mem_of_mem_nhds <| hy.mono_left h hs

theorem Specializes.map (h : x â¤³ y) (hf : Continuous f) : f x â¤³ f y :=
  h.map_of_continuousAt hf.continuousAt

theorem Inducing.specializes_iff (hf : Inducing f) : f x â¤³ f y â†” x â¤³ y := by
  simp only [specializes_iff_mem_closure, hf.closure_eq_preimage_closure_image, image_singleton,
    mem_preimage]

theorem subtype_specializes_iff {p : X â†’ Prop} (x y : Subtype p) : x â¤³ y â†” (x : X) â¤³ y :=
  inducing_subtype_val.specializes_iff.symm

@[simp]
theorem specializes_prod {xâ‚ xâ‚‚ : X} {yâ‚ yâ‚‚ : Y} : (xâ‚, yâ‚) â¤³ (xâ‚‚, yâ‚‚) â†” xâ‚ â¤³ xâ‚‚ âˆ§ yâ‚ â¤³ yâ‚‚ := by
  simp only [Specializes, nhds_prod_eq, prod_le_prod]

theorem Specializes.prod {xâ‚ xâ‚‚ : X} {yâ‚ yâ‚‚ : Y} (hx : xâ‚ â¤³ xâ‚‚) (hy : yâ‚ â¤³ yâ‚‚) :
    (xâ‚, yâ‚) â¤³ (xâ‚‚, yâ‚‚) :=
  specializes_prod.2 âŸ¨hx, hyâŸ©

@[simp]
theorem specializes_pi {f g : âˆ€ i, Ï€ i} : f â¤³ g â†” âˆ€ i, f i â¤³ g i := by
  simp only [Specializes, nhds_pi, pi_le_pi]

theorem not_specializes_iff_exists_open : Â¬x â¤³ y â†” âˆƒ S : Set X, IsOpen S âˆ§ y âˆˆ S âˆ§ x âˆ‰ S := by
  rw [specializes_iff_forall_open]
  push_neg
  rfl

theorem not_specializes_iff_exists_closed : Â¬x â¤³ y â†” âˆƒ S : Set X, IsClosed S âˆ§ x âˆˆ S âˆ§ y âˆ‰ S := by
  rw [specializes_iff_forall_closed]
  push_neg
  rfl

theorem IsOpen.continuous_piecewise_of_specializes [DecidablePred (Â· âˆˆ s)] (hs : IsOpen s)
    (hf : Continuous f) (hg : Continuous g) (hspec : âˆ€ x, f x â¤³ g x) :
    Continuous (s.piecewise f g) := by
  have : âˆ€ U, IsOpen U â†’ g â»Â¹' U âŠ† f â»Â¹' U := fun U hU x hx â†¦ (hspec x).mem_open hU hx
  rw [continuous_def]
  intro U hU
  rw [piecewise_preimage, ite_eq_of_subset_right _ (this U hU)]
  exact hU.preimage hf |>.inter hs |>.union (hU.preimage hg)

theorem IsClosed.continuous_piecewise_of_specializes [DecidablePred (Â· âˆˆ s)] (hs : IsClosed s)
    (hf : Continuous f) (hg : Continuous g) (hspec : âˆ€ x, g x â¤³ f x) :
    Continuous (s.piecewise f g) := by
  simpa only [piecewise_compl] using hs.isOpen_compl.continuous_piecewise_of_specializes hg hf hspec

variable (X)

/-- Specialization forms a preorder on the topological space. -/
def specializationPreorder : Preorder X :=
  { Preorder.lift (OrderDual.toDual âˆ˜ ğ“) with
    le := fun x y => y â¤³ x
    lt := fun x y => y â¤³ x âˆ§ Â¬x â¤³ y }

variable {X}

/-- A continuous function is monotone with respect to the specialization preorders on the domain and
the codomain. -/
theorem Continuous.specialization_monotone (hf : Continuous f) :
    @Monotone _ _ (specializationPreorder X) (specializationPreorder Y) f := fun _ _ h => h.map hf

/-!
### `Inseparable` relation
-/

/-- Two points `x` and `y` in a topological space are `Inseparable` if any of the following
equivalent properties hold:

- `ğ“ x = ğ“ y`; we use this property as the definition;
- for any open set `s`, `x âˆˆ s â†” y âˆˆ s`, see `inseparable_iff_open`;
- for any closed set `s`, `x âˆˆ s â†” y âˆˆ s`, see `inseparable_iff_closed`;
- `x âˆˆ closure {y}` and `y âˆˆ closure {x}`, see `inseparable_iff_mem_closure`;
- `closure {x} = closure {y}`, see `inseparable_iff_closure_eq`.
-/
def Inseparable (x y : X) : Prop :=
  ğ“ x = ğ“ y

local infixl:0 " ~áµ¢ " => Inseparable

theorem inseparable_def : (x ~áµ¢ y) â†” ğ“ x = ğ“ y :=
  Iff.rfl

theorem inseparable_iff_specializes_and : (x ~áµ¢ y) â†” x â¤³ y âˆ§ y â¤³ x :=
  le_antisymm_iff

theorem Inseparable.specializes (h : x ~áµ¢ y) : x â¤³ y := h.le

theorem Inseparable.specializes' (h : x ~áµ¢ y) : y â¤³ x := h.ge

theorem Specializes.antisymm (hâ‚ : x â¤³ y) (hâ‚‚ : y â¤³ x) : x ~áµ¢ y :=
  le_antisymm hâ‚ hâ‚‚

theorem inseparable_iff_forall_open : (x ~áµ¢ y) â†” âˆ€ s : Set X, IsOpen s â†’ (x âˆˆ s â†” y âˆˆ s) := by
  simp only [inseparable_iff_specializes_and, specializes_iff_forall_open, â† forall_and, â† iff_def,
    Iff.comm]

theorem not_inseparable_iff_exists_open :
    Â¬(x ~áµ¢ y) â†” âˆƒ s : Set X, IsOpen s âˆ§ Xor' (x âˆˆ s) (y âˆˆ s) :=
  by simp [inseparable_iff_forall_open, â† xor_iff_not_iff]

theorem inseparable_iff_forall_closed : (x ~áµ¢ y) â†” âˆ€ s : Set X, IsClosed s â†’ (x âˆˆ s â†” y âˆˆ s) := by
  simp only [inseparable_iff_specializes_and, specializes_iff_forall_closed, â† forall_and, â†
    iff_def]

theorem inseparable_iff_mem_closure :
    (x ~áµ¢ y) â†” x âˆˆ closure ({y} : Set X) âˆ§ y âˆˆ closure ({x} : Set X) :=
  inseparable_iff_specializes_and.trans <| by simp only [specializes_iff_mem_closure, and_comm]

theorem inseparable_iff_closure_eq : (x ~áµ¢ y) â†” closure ({x} : Set X) = closure {y} := by
  simp only [inseparable_iff_specializes_and, specializes_iff_closure_subset, â† subset_antisymm_iff,
    eq_comm]

theorem inseparable_of_nhdsWithin_eq (hx : x âˆˆ s) (hy : y âˆˆ s) (h : ğ“[s] x = ğ“[s] y) : x ~áµ¢ y :=
  (specializes_of_nhdsWithin h.le hx).antisymm (specializes_of_nhdsWithin h.ge hy)

theorem Inducing.inseparable_iff (hf : Inducing f) : (f x ~áµ¢ f y) â†” (x ~áµ¢ y) := by
  simp only [inseparable_iff_specializes_and, hf.specializes_iff]

theorem subtype_inseparable_iff {p : X â†’ Prop} (x y : Subtype p) : (x ~áµ¢ y) â†” ((x : X) ~áµ¢ y) :=
  inducing_subtype_val.inseparable_iff.symm

@[simp] theorem inseparable_prod {xâ‚ xâ‚‚ : X} {yâ‚ yâ‚‚ : Y} :
    ((xâ‚, yâ‚) ~áµ¢ (xâ‚‚, yâ‚‚)) â†” (xâ‚ ~áµ¢ xâ‚‚) âˆ§ (yâ‚ ~áµ¢ yâ‚‚) :=
  by simp only [Inseparable, nhds_prod_eq, prod_inj]

theorem Inseparable.prod {xâ‚ xâ‚‚ : X} {yâ‚ yâ‚‚ : Y} (hx : xâ‚ ~áµ¢ xâ‚‚) (hy : yâ‚ ~áµ¢ yâ‚‚) :
    (xâ‚, yâ‚) ~áµ¢ (xâ‚‚, yâ‚‚) :=
  inseparable_prod.2 âŸ¨hx, hyâŸ©

@[simp]
theorem inseparable_pi {f g : âˆ€ i, Ï€ i} : (f ~áµ¢ g) â†” âˆ€ i, f i ~áµ¢ g i := by
  simp only [Inseparable, nhds_pi, funext_iff, pi_inj]

namespace Inseparable

@[refl]
theorem refl (x : X) : x ~áµ¢ x :=
  Eq.refl (ğ“ x)

theorem rfl : x ~áµ¢ x :=
  refl x

theorem of_eq (e : x = y) : Inseparable x y :=
  e â–¸ refl x

@[symm]
nonrec theorem symm (h : x ~áµ¢ y) : y ~áµ¢ x := h.symm

@[trans]
nonrec theorem trans (hâ‚ : x ~áµ¢ y) (hâ‚‚ : y ~áµ¢ z) : x ~áµ¢ z := hâ‚.trans hâ‚‚

theorem nhds_eq (h : x ~áµ¢ y) : ğ“ x = ğ“ y := h

theorem mem_open_iff (h : x ~áµ¢ y) (hs : IsOpen s) : x âˆˆ s â†” y âˆˆ s :=
  inseparable_iff_forall_open.1 h s hs

theorem mem_closed_iff (h : x ~áµ¢ y) (hs : IsClosed s) : x âˆˆ s â†” y âˆˆ s :=
  inseparable_iff_forall_closed.1 h s hs

theorem map_of_continuousAt (h : x ~áµ¢ y) (hx : ContinuousAt f x) (hy : ContinuousAt f y) :
    f x ~áµ¢ f y :=
  (h.specializes.map_of_continuousAt hy).antisymm (h.specializes'.map_of_continuousAt hx)

theorem map (h : x ~áµ¢ y) (hf : Continuous f) : f x ~áµ¢ f y :=
  h.map_of_continuousAt hf.continuousAt hf.continuousAt

end Inseparable

theorem IsClosed.not_inseparable (hs : IsClosed s) (hx : x âˆˆ s) (hy : y âˆ‰ s) : Â¬(x ~áµ¢ y) := fun h =>
  hy <| (h.mem_closed_iff hs).1 hx

theorem IsOpen.not_inseparable (hs : IsOpen s) (hx : x âˆˆ s) (hy : y âˆ‰ s) : Â¬(x ~áµ¢ y) := fun h =>
  hy <| (h.mem_open_iff hs).1 hx

/-!
### Separation quotient

In this section we define the quotient of a topological space by the `Inseparable` relation.
-/


variable (X)

/-- A `setoid` version of `Inseparable`, used to define the `SeparationQuotient`. -/
def inseparableSetoid : Setoid X := { Setoid.comap ğ“ âŠ¥ with r := Inseparable }

/-- The quotient of a topological space by its `inseparableSetoid`. This quotient is guaranteed to
be a Tâ‚€ space. -/
def SeparationQuotient := Quotient (inseparableSetoid X)

instance : TopologicalSpace (SeparationQuotient X) := instTopologicalSpaceQuotient

variable {X}
variable {t : Set (SeparationQuotient X)}

namespace SeparationQuotient

/-- The natural map from a topological space to its separation quotient. -/
def mk : X â†’ SeparationQuotient X := Quotient.mk''

theorem quotientMap_mk : QuotientMap (mk : X â†’ SeparationQuotient X) :=
  quotientMap_quot_mk

theorem continuous_mk : Continuous (mk : X â†’ SeparationQuotient X) :=
  continuous_quot_mk

@[simp]
theorem mk_eq_mk : mk x = mk y â†” (x ~áµ¢ y) :=
  Quotient.eq''

theorem surjective_mk : Surjective (mk : X â†’ SeparationQuotient X) :=
  surjective_quot_mk _

@[simp]
theorem range_mk : range (mk : X â†’ SeparationQuotient X) = univ :=
  surjective_mk.range_eq

instance [Nonempty X] : Nonempty (SeparationQuotient X) :=
  Nonempty.map mk â€¹_â€º

instance [Inhabited X] : Inhabited (SeparationQuotient X) :=
  âŸ¨mk defaultâŸ©

instance [Subsingleton X] : Subsingleton (SeparationQuotient X) :=
  surjective_mk.subsingleton

theorem preimage_image_mk_open (hs : IsOpen s) : mk â»Â¹' (mk '' s) = s := by
  refine' Subset.antisymm _ (subset_preimage_image _ _)
  rintro x âŸ¨y, hys, hxyâŸ©
  exact ((mk_eq_mk.1 hxy).mem_open_iff hs).1 hys

theorem isOpenMap_mk : IsOpenMap (mk : X â†’ SeparationQuotient X) := fun s hs =>
  quotientMap_mk.isOpen_preimage.1 <| by rwa [preimage_image_mk_open hs]

theorem preimage_image_mk_closed (hs : IsClosed s) : mk â»Â¹' (mk '' s) = s := by
  refine' Subset.antisymm _ (subset_preimage_image _ _)
  rintro x âŸ¨y, hys, hxyâŸ©
  exact ((mk_eq_mk.1 hxy).mem_closed_iff hs).1 hys

theorem inducing_mk : Inducing (mk : X â†’ SeparationQuotient X) :=
  âŸ¨le_antisymm (continuous_iff_le_induced.1 continuous_mk) fun s hs =>
      âŸ¨mk '' s, isOpenMap_mk s hs, preimage_image_mk_open hsâŸ©âŸ©

theorem isClosedMap_mk : IsClosedMap (mk : X â†’ SeparationQuotient X) :=
  inducing_mk.isClosedMap <| by rw [range_mk]; exact isClosed_univ

@[simp]
theorem comap_mk_nhds_mk : comap mk (ğ“ (mk x)) = ğ“ x :=
  (inducing_mk.nhds_eq_comap _).symm

@[simp]
theorem comap_mk_nhdsSet_image : comap mk (ğ“Ë¢ (mk '' s)) = ğ“Ë¢ s :=
  (inducing_mk.nhdsSet_eq_comap _).symm

theorem map_mk_nhds : map mk (ğ“ x) = ğ“ (mk x) := by
  rw [â† comap_mk_nhds_mk, map_comap_of_surjective surjective_mk]

theorem map_mk_nhdsSet : map mk (ğ“Ë¢ s) = ğ“Ë¢ (mk '' s) := by
  rw [â† comap_mk_nhdsSet_image, map_comap_of_surjective surjective_mk]

theorem comap_mk_nhdsSet : comap mk (ğ“Ë¢ t) = ğ“Ë¢ (mk â»Â¹' t) := by
  conv_lhs => rw [â† image_preimage_eq t surjective_mk, comap_mk_nhdsSet_image]

theorem preimage_mk_closure : mk â»Â¹' closure t = closure (mk â»Â¹' t) :=
  isOpenMap_mk.preimage_closure_eq_closure_preimage continuous_mk t

theorem preimage_mk_interior : mk â»Â¹' interior t = interior (mk â»Â¹' t) :=
  isOpenMap_mk.preimage_interior_eq_interior_preimage continuous_mk t

theorem preimage_mk_frontier : mk â»Â¹' frontier t = frontier (mk â»Â¹' t) :=
  isOpenMap_mk.preimage_frontier_eq_frontier_preimage continuous_mk t

theorem image_mk_closure : mk '' closure s = closure (mk '' s) :=
  (image_closure_subset_closure_image continuous_mk).antisymm <|
    isClosedMap_mk.closure_image_subset _

theorem map_prod_map_mk_nhds (x : X) (y : Y) : map (Prod.map mk mk) (ğ“ (x, y)) = ğ“ (mk x, mk y) :=
  by rw [nhds_prod_eq, â† prod_map_map_eq', map_mk_nhds, map_mk_nhds, nhds_prod_eq]

theorem map_mk_nhdsWithin_preimage (s : Set (SeparationQuotient X)) (x : X) :
    map mk (ğ“[mk â»Â¹' s] x) = ğ“[s] mk x := by
  rw [nhdsWithin, â† comap_principal, Filter.push_pull, nhdsWithin, map_mk_nhds]

/-- Lift a map `f : X â†’ Î±` such that `Inseparable x y â†’ f x = f y` to a map
`SeparationQuotient X â†’ Î±`. -/
def lift (f : X â†’ Î±) (hf : âˆ€ x y, (x ~áµ¢ y) â†’ f x = f y) : SeparationQuotient X â†’ Î± := fun x =>
  Quotient.liftOn' x f hf

@[simp]
theorem lift_mk {f : X â†’ Î±} (hf : âˆ€ x y, (x ~áµ¢ y) â†’ f x = f y) (x : X) : lift f hf (mk x) = f x :=
  rfl

@[simp]
theorem lift_comp_mk {f : X â†’ Î±} (hf : âˆ€ x y, (x ~áµ¢ y) â†’ f x = f y) : lift f hf âˆ˜ mk = f :=
  rfl

@[simp]
theorem tendsto_lift_nhds_mk {f : X â†’ Î±} {hf : âˆ€ x y, (x ~áµ¢ y) â†’ f x = f y} {x : X} {l : Filter Î±} :
    Tendsto (lift f hf) (ğ“ <| mk x) l â†” Tendsto f (ğ“ x) l := by
  simp only [â† map_mk_nhds, tendsto_map'_iff, lift_comp_mk]

@[simp]
theorem tendsto_lift_nhdsWithin_mk {f : X â†’ Î±} {hf : âˆ€ x y, (x ~áµ¢ y) â†’ f x = f y} {x : X}
    {s : Set (SeparationQuotient X)} {l : Filter Î±} :
    Tendsto (lift f hf) (ğ“[s] mk x) l â†” Tendsto f (ğ“[mk â»Â¹' s] x) l := by
  simp only [â† map_mk_nhdsWithin_preimage, tendsto_map'_iff, lift_comp_mk]

@[simp]
theorem continuousAt_lift {f : X â†’ Y} {hf : âˆ€ x y, (x ~áµ¢ y) â†’ f x = f y} {x : X} :
    ContinuousAt (lift f hf) (mk x) â†” ContinuousAt f x :=
  tendsto_lift_nhds_mk

@[simp]
theorem continuousWithinAt_lift {f : X â†’ Y} {hf : âˆ€ x y, (x ~áµ¢ y) â†’ f x = f y}
    {s : Set (SeparationQuotient X)} {x : X} :
    ContinuousWithinAt (lift f hf) s (mk x) â†” ContinuousWithinAt f (mk â»Â¹' s) x :=
  tendsto_lift_nhdsWithin_mk

@[simp]
theorem continuousOn_lift {f : X â†’ Y} {hf : âˆ€ x y, (x ~áµ¢ y) â†’ f x = f y}
    {s : Set (SeparationQuotient X)} : ContinuousOn (lift f hf) s â†” ContinuousOn f (mk â»Â¹' s) := by
  simp only [ContinuousOn, surjective_mk.forall, continuousWithinAt_lift, mem_preimage]

@[simp]
theorem continuous_lift {f : X â†’ Y} {hf : âˆ€ x y, (x ~áµ¢ y) â†’ f x = f y} :
    Continuous (lift f hf) â†” Continuous f := by
  simp only [continuous_iff_continuousOn_univ, continuousOn_lift, preimage_univ]

/-- Lift a map `f : X â†’ Y â†’ Î±` such that `Inseparable a b â†’ Inseparable c d â†’ f a c = f b d` to a
map `SeparationQuotient X â†’ SeparationQuotient Y â†’ Î±`. -/
def liftâ‚‚ (f : X â†’ Y â†’ Î±) (hf : âˆ€ a b c d, (a ~áµ¢ c) â†’ (b ~áµ¢ d) â†’ f a b = f c d) :
    SeparationQuotient X â†’ SeparationQuotient Y â†’ Î± := fun x y => Quotient.liftOnâ‚‚' x y f hf

@[simp]
theorem liftâ‚‚_mk {f : X â†’ Y â†’ Î±} (hf : âˆ€ a b c d, (a ~áµ¢ c) â†’ (b ~áµ¢ d) â†’ f a b = f c d) (x : X)
    (y : Y) : liftâ‚‚ f hf (mk x) (mk y) = f x y :=
  rfl

@[simp]
theorem tendsto_liftâ‚‚_nhds {f : X â†’ Y â†’ Î±} {hf : âˆ€ a b c d, (a ~áµ¢ c) â†’ (b ~áµ¢ d) â†’ f a b = f c d}
    {x : X} {y : Y} {l : Filter Î±} :
    Tendsto (uncurry <| liftâ‚‚ f hf) (ğ“ (mk x, mk y)) l â†” Tendsto (uncurry f) (ğ“ (x, y)) l := by
  rw [â† map_prod_map_mk_nhds, tendsto_map'_iff]
  rfl

@[simp] theorem tendsto_liftâ‚‚_nhdsWithin {f : X â†’ Y â†’ Î±}
    {hf : âˆ€ a b c d, (a ~áµ¢ c) â†’ (b ~áµ¢ d) â†’ f a b = f c d} {x : X} {y : Y}
    {s : Set (SeparationQuotient X Ã— SeparationQuotient Y)} {l : Filter Î±} :
    Tendsto (uncurry <| liftâ‚‚ f hf) (ğ“[s] (mk x, mk y)) l â†”
      Tendsto (uncurry f) (ğ“[Prod.map mk mk â»Â¹' s] (x, y)) l := by
  rw [nhdsWithin, â† map_prod_map_mk_nhds, â† Filter.push_pull, comap_principal]
  rfl

@[simp]
theorem continuousAt_liftâ‚‚ {f : X â†’ Y â†’ Z} {hf : âˆ€ a b c d, (a ~áµ¢ c) â†’ (b ~áµ¢ d) â†’ f a b = f c d}
    {x : X} {y : Y} :
    ContinuousAt (uncurry <| liftâ‚‚ f hf) (mk x, mk y) â†” ContinuousAt (uncurry f) (x, y) :=
  tendsto_liftâ‚‚_nhds

@[simp] theorem continuousWithinAt_liftâ‚‚ {f : X â†’ Y â†’ Z}
    {hf : âˆ€ a b c d, (a ~áµ¢ c) â†’ (b ~áµ¢ d) â†’ f a b = f c d}
    {s : Set (SeparationQuotient X Ã— SeparationQuotient Y)} {x : X} {y : Y} :
    ContinuousWithinAt (uncurry <| liftâ‚‚ f hf) s (mk x, mk y) â†”
      ContinuousWithinAt (uncurry f) (Prod.map mk mk â»Â¹' s) (x, y) :=
  tendsto_liftâ‚‚_nhdsWithin

@[simp]
theorem continuousOn_liftâ‚‚ {f : X â†’ Y â†’ Z} {hf : âˆ€ a b c d, (a ~áµ¢ c) â†’ (b ~áµ¢ d) â†’ f a b = f c d}
    {s : Set (SeparationQuotient X Ã— SeparationQuotient Y)} :
    ContinuousOn (uncurry <| liftâ‚‚ f hf) s â†” ContinuousOn (uncurry f) (Prod.map mk mk â»Â¹' s) := by
  simp_rw [ContinuousOn, (surjective_mk.Prod_map surjective_mk).forall, Prod.forall, Prod.map,
    continuousWithinAt_liftâ‚‚]
  rfl

@[simp]
theorem continuous_liftâ‚‚ {f : X â†’ Y â†’ Z} {hf : âˆ€ a b c d, (a ~áµ¢ c) â†’ (b ~áµ¢ d) â†’ f a b = f c d} :
    Continuous (uncurry <| liftâ‚‚ f hf) â†” Continuous (uncurry f) := by
  simp only [continuous_iff_continuousOn_univ, continuousOn_liftâ‚‚, preimage_univ]

end SeparationQuotient

theorem continuous_congr_of_inseparable (h : âˆ€ x, f x ~áµ¢ g x) :
    Continuous f â†” Continuous g := by
  simp_rw [SeparationQuotient.inducing_mk.continuous_iff (Î² := Y)]
  exact continuous_congr fun x â†¦ SeparationQuotient.mk_eq_mk.mpr (h x)
