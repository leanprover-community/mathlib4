/-
Copyright (c) 2025 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne, David Ledvinka
-/
import Mathlib.Analysis.SpecialFunctions.Log.ENNRealLogExp
import Mathlib.Order.CompletePartialOrder

/-!
# Pair Reduction

The goal of this file is to prove the theorem `pair_reduction`. This is essentially Lemma 6.1 in
[kratschmer_urusov2023] which is an extension of Lemma B.2.7. in [talagrand2014].
Given pseudometric spaces `T` and `E`, `c ‚â• 0`, and a finite subset `J` of `T` such that
`|J| ‚â§ a‚Åø` for some `a ‚â• 0` and `n : ‚Ñï`, `pair_reduction` states that there exists a set `K ‚äÜ J¬≤`
such that for any function `f : T ‚Üí E`:

1. `|K| ‚â§ a|J|`
2. `‚àÄ (s, t) ‚àà K, d(s, t) ‚â§ cn`
3. `sup_{s, t ‚àà J : d(s, t) ‚â§ c} d(f(s), f(t)) ‚â§ 2 sup_{(s, t) ‚àà K} d(f(s), f(t))`

When applying the chaining technique for bounding the supremum of the incremements of stochastic
processes, `pair_reduction` is used to reduce the order of the dependence of the bound on the
covering numbers of the pseudometric space. As a simple example of how it could be used, suppose
`T` has an `Œµ`-covering number `N` and suppose `J` is an `Œµ`-covering of `T` with `|J| = N`.
Let `f : Œ© ‚Üí T ‚Üí E` be any stochastic process such that `ùîº d(f(s), f(t)) ‚â§ d (s, t)` for all
`s, t ‚àà T`. Then naively
```
  ùîº[sup_{(s, t) ‚àà J} : d(s, t) ‚â§ c} d(f(s), f(t))]
    ‚â§ ‚àë_{(s, t) ‚àà J¬≤ : d(s, t) ‚â§ c} ùîº[d(f(s), f(t))]
    ‚â§ |J|¬≤ c
    = c N¬≤
```
but applying `pair_reduction` with `n = log |J|` we get
```
  ùîº[sup_{(s, t) ‚àà J : d(s, t) ‚â§ c} d(f(s), f(t))]
    ‚â§ 2 ùîº[sup_{(s, t) ‚àà K} d(f(s), f(t))]
    ‚â§ 2 ‚àë_{(s, t) ‚àà K} ùîº[d(f(s), f(t))]
    ‚â§ 2 a |J| c log |J|
    ‚â§ 2 a c N log N
```
`pair_reduction` is used in [kratschmer_urusov2023] to prove a form of the Kolmogorov-Chentsov
theorem that applies to stochastic processses which satisfy the Kolmogorov condition but works
on very general metric spaces.

## Implementation

In this section we sketch a proof of `pair_reduction` with references to the corresponding steps
in the lean code.

For any `V : Finset T` and `t : T` we define the log-size radius of `t` in `V` to be the smallest
natural number `k` greater than zero such that `|{x ‚àà V | d(t, x) ‚â§ kc}| ‚â§ a·µè`.
(see `logSizeRadius`)

We construct a sequence `V·µ¢` of subsets of `J`, a sequence `t·µ¢ ‚àà V·µ¢` and a sequence of `r·µ¢ : ‚Ñï`
inductively as follows (see `logSizeBallSeq`):

* `V‚ÇÄ = J`, `t‚Çí` is chosen arbitarily in `J`, `r‚ÇÄ` is the log-size radius of `t‚ÇÄ` in `V‚ÇÄ`
* `V·µ¢‚Çä·µ¢ = V·µ¢ \ B·µ¢` where `B·µ¢ := {x ‚àà V | d(t, x) ‚â§ (r·µ¢ - 1) c}`, `t·µ¢‚Çä‚ÇÅ` is chosen arbitarily in
  `V·µ¢‚Çä‚ÇÅ` (if it is nonempty), `r·µ¢‚Çä‚ÇÅ` is the log-size radius of `t·µ¢‚Çä‚ÇÅ` in `V·µ¢‚Çä·µ¢`.

Then `V·µ¢` is a strictly decreasing sequence (see `card_finset_logSizeBallSeq_add_one_lt`) until
`V·µ¢` is empty. In particular `V·µ¢ = ‚àÖ` for `i ‚â• |J|`
(see `card_finset_logSizeBallSeq_card_eq_zero`).

We will show that `K = ‚ãÉ_{i=1}^|J| {t·µ¢} √ó {x ‚àà V·µ¢ | d(t·µ¢, x) ‚â§ cr·µ¢}` suffices
(see `pairSet` and `pairSetSeq`).

To prove (1) we have that
```
  |K| ‚â§ ‚àë_{i=0}^|J| |{x ‚àà V·µ¢ : d(t, x) ‚â§ cr·µ¢}|
      ‚â§ ‚àë_{i=0}^|J| a ^ r·µ¢  (by definition of `r·µ¢`)
      = a ‚àë_{i=0}^|J| a ^ (r·µ¢ - 1)
      ‚â§ a ‚àë_{i=0}^|J| |B·µ¢| (by definition of `r·µ¢`)
      ‚â§ a |J| (since the `B·µ¢` are disjoint (see `disjoint_smallBall_logSizeBallSeq`))
```
(see `card_pairSet_le`).

(2) follows easily from the definition of K and the fact that `r·µ¢ ‚â§ n` for each `i`
(see `edist_le_of_mem_pairSet` and `radius_logSizeBallSeq_le`)

Finally we prove (3). Let `s, t ‚àà J` such that `d(s, t) ‚â§ c`. Let `i` be the largest integer
such that both `s, t ‚àà V·µ¢`. WLOG suppose `s ‚àâ V·µ¢‚Çä‚ÇÅ` so that in particular `s ‚àà B·µ¢` which means
by definition that `d(t·µ¢, s) ‚â§ (r·µ¢ - 1)c`. Then we also have
```
d(t·µ¢, t) ‚â§ d(t·µ¢, s) + d(s, t) ‚â§ (r·µ¢ - 1)c + c = r·µ¢c
```
hence `(t·µ¢, s), (t·µ¢, t) ‚àà K`. Furthermore
```
d(f(s), f(t)) ‚â§ d(f(t·µ¢), f(s)) + d(f(t·µ¢), f(t))
```
taking supremums completes the proof (see `iSup_edist_pairSet`).

## References

* [V. Kr√§tschmer, M. Urusov, *A Kolmogorov‚ÄìChentsov Type Theorem on General Metric Spaces with
  Applications to Limit Theorems for Banach-Valued Processes*][kratschmer_urusov2023]
* [M. Talagrand, *Upper and Lower Bounds for Stochastic Processes*][talagrand2014]

-/

open scoped ENNReal NNReal Finset

variable {T : Type*} [PseudoEMetricSpace T] {a c : ‚Ñù‚â•0‚àû} {n : ‚Ñï} {V J : Finset T} {t : T}

namespace PairReduction

lemma exists_radius_le (t : T) (V : Finset T) (ha : 1 < a) (c : ‚Ñù‚â•0‚àû) :
    ‚àÉ r : ‚Ñï, 1 ‚â§ r ‚àß #(V.filter fun x ‚Ü¶ edist t x ‚â§ r * c) ‚â§ a ^ r := by
  have := ENNReal.tendsto_nhds_top_iff_nat.1
    ((ENNReal.tendsto_rpow_atTop_of_one_lt_base ha).comp tendsto_natCast_atTop_atTop) #V
  simp only [Function.comp_apply, ENNReal.rpow_natCast, Filter.eventually_atTop, ge_iff_le] at this
  obtain ‚ü®r, hr‚ü© := this
  exact ‚ü®max r 1, le_max_right r 1,
    le_trans (mod_cast Finset.card_filter_le V _) (hr (max r 1) (le_max_left r 1)).le‚ü©

/-- The log-size radius of `t` in `V` is the smallest natural number n greater than zero such that
 `|{x ‚àà V | d(t, x) ‚â§ nc}| ‚â§ a‚Åø`. -/
noncomputable
def logSizeRadius (t : T) (V : Finset T) (a c : ‚Ñù‚â•0‚àû) : ‚Ñï :=
  if h : 1 < a then Nat.find (exists_radius_le t V h c) else 0

lemma one_le_logSizeRadius (ha : 1 < a) :
    1 ‚â§ logSizeRadius t V a c := by
  rw [logSizeRadius, dif_pos ha]
  exact (Nat.find_spec (exists_radius_le t V ha c)).1

lemma card_le_logSizeRadius_le_pow_logSizeRadius (ha : 1 < a) :
    #(V.filter fun x ‚Ü¶ edist t x ‚â§ logSizeRadius t V a c * c) ‚â§ a ^ (logSizeRadius t V a c) := by
  rw [logSizeRadius, dif_pos ha]
  exact (Nat.find_spec (exists_radius_le t V ha c)).2

lemma pow_logSizeRadius_le_card_le_logSizeRadius (ha : 1 < a) (ht : t ‚àà V) :
    a ^ (logSizeRadius t V a c - 1)
      ‚â§ #(V.filter fun x ‚Ü¶ edist t x ‚â§ (logSizeRadius t V a c - 1) * c) := by
  by_cases h_one : logSizeRadius t V a c = 1
  ¬∑ simp only [h_one, tsub_self, pow_zero, Nat.cast_one, zero_mul, nonpos_iff_eq_zero,
      Nat.one_le_cast, Finset.one_le_card]
    exact ‚ü®t, by simpa‚ü©
  rw [logSizeRadius, dif_pos ha] at h_one ‚ä¢
  have : Nat.find (exists_radius_le t V ha c) - 1 < Nat.find (exists_radius_le t V ha c) := by
    simp
  have h := Nat.find_min (exists_radius_le t V ha c) this
  simp only [ENNReal.natCast_sub, Nat.cast_one, not_and, not_le] at h
  exact (h (by omega)).le

/-- A structure for carrying the data of `logSizeBallSeq` -/
structure logSizeBallStruct (T : Type*) where
  /-- The underlying finite set of a `logSizeBallStruct` -/
  finset : Finset T
  /-- The underlying point of a `logSizeBallStruct` (typically a point in the underlying finite
  set) -/
  point : T
  /-- The underlying radius of a `logSizeBallStruct` (typically the log-size radius of the
  underlying point in the underlying finite set) -/
  radius : ‚Ñï

/-- If `(V, t, r)` is a `logSizeBallStruct` then `logSizeBallStruct.smallBall`
  is `{x ‚àà V | d(t, x) ‚â§ (r - 1)c}`. -/
noncomputable
def logSizeBallStruct.smallBall (struct : logSizeBallStruct T) (c : ‚Ñù‚â•0‚àû) :
    Finset T :=
  struct.finset.filter fun x ‚Ü¶ edist struct.point x ‚â§ (struct.radius - 1) * c

/-- If `(V, t, r)` is a `logSizeBallStruct` then `logSizeBallStruct.ball`
  is `{x ‚àà V | d(t, x) ‚â§ rc}`. -/
noncomputable
def logSizeBallStruct.ball (struct : logSizeBallStruct T) (c : ‚Ñù‚â•0‚àû) :
    Finset T :=
  struct.finset.filter fun x ‚Ü¶ edist struct.point x ‚â§ struct.radius * c

variable [DecidableEq T]

/-- We recursively define a log-size ball sequence `(V·µ¢, t·µ¢, r·µ¢)` by
  * `V‚ÇÄ = J`, `t‚Çí` is chosen arbitarily in `J`, `r‚ÇÄ` is the log-size radius of `t‚ÇÄ` in `V‚ÇÄ`
  * `V·µ¢‚Çä·µ¢ = V·µ¢ \ {x ‚àà V | d(t, x) ‚â§ (r·µ¢ - 1)c}`, `t·µ¢‚Çä‚ÇÅ` is chosen arbitarily in `V·µ¢‚Çä‚ÇÅ, r·µ¢‚Çä‚ÇÅ` is
    the log-size radius of `t·µ¢‚Çä‚ÇÅ` in `V·µ¢‚Çä·µ¢`. -/
noncomputable
def logSizeBallSeq (J : Finset T) (hJ : J.Nonempty) (a c : ‚Ñù‚â•0‚àû) : ‚Ñï ‚Üí logSizeBallStruct T
  | 0 => {finset := J, point := hJ.choose, radius := logSizeRadius hJ.choose J a c}
  | n + 1 =>
    let V' := (logSizeBallSeq J hJ a c n).finset \ ((logSizeBallSeq J hJ a c n).smallBall c)
    let t' := if hV' : V'.Nonempty then hV'.choose else (logSizeBallSeq J hJ a c n).point
    { finset := V',
      point := t',
      radius := logSizeRadius t' V' a c }

lemma finset_logSizeBallSeq_zero (hJ : J.Nonempty) :
    (logSizeBallSeq J hJ a c 0).finset = J := rfl

lemma point_logSizeBallSeq_zero (hJ : J.Nonempty) :
    (logSizeBallSeq J hJ a c 0).point = hJ.choose := rfl

lemma radius_logSizeBallSeq_zero (hJ : J.Nonempty) :
    (logSizeBallSeq J hJ a c 0).radius = logSizeRadius hJ.choose J a c := rfl

lemma finset_logSizeBallSeq_add_one (hJ : J.Nonempty) (i : ‚Ñï) :
    (logSizeBallSeq J hJ a c (i + 1)).finset =
      (logSizeBallSeq J hJ a c i).finset \ (logSizeBallSeq J hJ a c i).smallBall c := rfl

lemma point_logSizeBallSeq_add_one (hJ : J.Nonempty) (i : ‚Ñï) :
    (logSizeBallSeq J hJ a c (i + 1)).point
      = if hV' : (logSizeBallSeq J hJ a c (i + 1)).finset.Nonempty then hV'.choose
        else (logSizeBallSeq J hJ a c i).point := rfl

lemma radius_logSizeBallSeq_add_one (hJ : J.Nonempty) (i : ‚Ñï) :
    (logSizeBallSeq J hJ a c (i + 1)).radius
      = logSizeRadius (logSizeBallSeq J hJ a c (i + 1)).point
          (logSizeBallSeq J hJ a c (i + 1)).finset a c := rfl

lemma finset_logSizeBallSeq_add_one_subset (hJ : J.Nonempty) (i : ‚Ñï) :
    (logSizeBallSeq J hJ a c (i + 1)).finset ‚äÜ (logSizeBallSeq J hJ a c i).finset := by
  simp [finset_logSizeBallSeq_add_one]

lemma antitone_logSizeBallSeq_add_one_subset (hJ : J.Nonempty) :
    Antitone (fun i ‚Ü¶ (logSizeBallSeq J hJ a c i).finset) :=
  antitone_nat_of_succ_le (finset_logSizeBallSeq_add_one_subset hJ)

lemma finset_logSizeBallSeq_subset_logSizeBallSeq_init (hJ : J.Nonempty) (i : ‚Ñï) :
    (logSizeBallSeq J hJ a c i).finset ‚äÜ J := by
  apply subset_trans <| antitone_logSizeBallSeq_add_one_subset hJ (zero_le i)
  simp [finset_logSizeBallSeq_zero]

lemma radius_logSizeBallSeq_le (hJ : J.Nonempty) (ha : 1 < a) (hn : 1 ‚â§ n) (hJ_card : #J ‚â§ a ^ n)
    (i : ‚Ñï) : (logSizeBallSeq J hJ a c i).radius ‚â§ n := by
  match i with
  | 0 =>
    simp only [radius_logSizeBallSeq_zero, logSizeRadius, ha, ‚ÜìreduceDIte]
    exact Nat.find_min' _ ‚ü®hn, le_trans (by gcongr; apply Finset.filter_subset) hJ_card‚ü©
  | i + 1 =>
    simp only [radius_logSizeBallSeq_add_one, logSizeRadius, ha, ‚ÜìreduceDIte]
    refine Nat.find_min' _ ‚ü®hn, le_trans ?_ hJ_card‚ü©
    gcongr
    exact (Finset.filter_subset _ _).trans (finset_logSizeBallSeq_subset_logSizeBallSeq_init _ _)

lemma one_le_radius_logSizeBallSeq (hJ : J.Nonempty) (ha : 1 < a) (i : ‚Ñï) :
    1 ‚â§ (logSizeBallSeq J hJ a c i).radius := by
  match i with
  | 0 => exact one_le_logSizeRadius ha
  | i + 1 => exact one_le_logSizeRadius ha

lemma point_mem_finset_logSizeBallSeq (hJ : J.Nonempty) (i : ‚Ñï)
    (h : (logSizeBallSeq J hJ a c i).finset.Nonempty) :
    (logSizeBallSeq J hJ a c i).point ‚àà (logSizeBallSeq J hJ a c i).finset := by
  match i with
  | 0 => simp [point_logSizeBallSeq_zero, finset_logSizeBallSeq_zero, Exists.choose_spec]
  | i + 1 => simp [point_logSizeBallSeq_add_one, h, Exists.choose_spec]

lemma point_mem_logSizeBallSeq_init (hJ : J.Nonempty) (i : ‚Ñï) :
    (logSizeBallSeq J hJ a c i).point ‚àà J := by
  induction i with
  | zero => exact point_mem_finset_logSizeBallSeq hJ 0 hJ
  | succ i ih =>
    by_cases h : (logSizeBallSeq J hJ a c (i + 1)).finset.Nonempty
    ¬∑ refine Finset.mem_of_subset ?_ (point_mem_finset_logSizeBallSeq hJ (i + 1) h)
      apply finset_logSizeBallSeq_subset_logSizeBallSeq_init
    simp [point_logSizeBallSeq_add_one, ih, h]

lemma point_notMem_finset_logSizeBallSeq_add_one (hJ : J.Nonempty) (i : ‚Ñï) :
    (logSizeBallSeq J hJ a c i).point ‚àâ (logSizeBallSeq J hJ a c (i + 1)).finset := by
  simp [finset_logSizeBallSeq_add_one, logSizeBallStruct.smallBall]

lemma finset_logSizeBallSeq_add_one_ssubset (hJ : J.Nonempty) (i : ‚Ñï)
    (h : (logSizeBallSeq J hJ a c i).finset.Nonempty) :
    (logSizeBallSeq J hJ a c (i + 1)).finset ‚äÇ (logSizeBallSeq J hJ a c i).finset := by
    apply ssubset_of_subset_not_subset
    ¬∑ simp [finset_logSizeBallSeq_add_one]
    refine Set.not_subset.mpr ‚ü®(logSizeBallSeq J hJ a c i).point, ?_, ?_‚ü©
    ¬∑ exact point_mem_finset_logSizeBallSeq hJ i h
    ¬∑ exact point_notMem_finset_logSizeBallSeq_add_one hJ i

lemma card_finset_logSizeBallSeq_add_one_lt (hJ : J.Nonempty) (i : ‚Ñï)
    (h : (logSizeBallSeq J hJ a c i).finset.Nonempty) :
    #(logSizeBallSeq J hJ a c (i + 1)).finset < #(logSizeBallSeq J hJ a c i).finset := by
  simp [Finset.card_lt_card, finset_logSizeBallSeq_add_one_ssubset hJ i h]

lemma card_finset_logSizeBallSeq_le (hJ : J.Nonempty) (i : ‚Ñï) :
    #(logSizeBallSeq J hJ a c i).finset ‚â§ #J - i := by
  induction i with
  | zero => simp [finset_logSizeBallSeq_zero]
  | succ i ih =>
    by_cases h : (logSizeBallSeq J hJ a c i).finset.Nonempty
    ¬∑ have := card_finset_logSizeBallSeq_add_one_lt hJ i h
      omega
    apply le_trans <| Finset.card_le_card (finset_logSizeBallSeq_add_one_subset hJ i)
    suffices #(logSizeBallSeq J hJ a c i).finset = 0 by simp [this]
    rwa [‚Üê not_ne_iff, Finset.card_ne_zero.not]

lemma card_finset_logSizeBallSeq_card_eq_zero (hJ : J.Nonempty) :
    #(logSizeBallSeq J hJ a c #J).finset = 0 := by
  rw [‚Üê Nat.le_zero, ‚Üê tsub_self #J]
  exact card_finset_logSizeBallSeq_le hJ #J

lemma disjoint_smallBall_logSizeBallSeq (hJ : J.Nonempty) {i j : ‚Ñï} (hij : i ‚â† j) :
    Disjoint
      ((logSizeBallSeq J hJ a c i).smallBall c) ((logSizeBallSeq J hJ a c j).smallBall c) := by
  wlog h : i < j generalizing i j
  ¬∑ exact Disjoint.symm <| this hij.symm <| (ne_iff_lt_iff_le.mpr (not_lt.mp h)).mp hij.symm
  apply Finset.disjoint_of_subset_right
  ¬∑ exact (Finset.filter_subset _ _).trans (antitone_logSizeBallSeq_add_one_subset hJ h)
  simp [finset_logSizeBallSeq_add_one, Finset.disjoint_sdiff]

/-- Given a log-size ball sequence `(V·µ¢, t·µ¢, r·µ¢)`, we define the pair set sequence by
`K·µ¢ = {t·µ¢} √ó {x ‚àà V·µ¢ | dist(t·µ¢, x) ‚â§ r·µ¢c}`. -/
noncomputable
def pairSetSeq (J : Finset T) (a c : ‚Ñù‚â•0‚àû) (n : ‚Ñï) : Finset (T √ó T) :=
  if hJ : J.Nonempty then
    Finset.product {(logSizeBallSeq J hJ a c n).point} ((logSizeBallSeq J hJ a c n).ball c)
  else ‚àÖ

/-- Given the pair set sequence K·µ¢ we define the pair set `K` by `K = ‚ãÉ i, K·µ¢`. -/
noncomputable
def pairSet (J : Finset T) (a c : ‚Ñù‚â•0‚àû) : Finset (T √ó T) :=
  Finset.biUnion (Finset.range #J) (pairSetSeq J a c)

lemma pairSet_empty_eq_empty (a c : ‚Ñù‚â•0‚àû) : pairSet (‚àÖ : Finset T) a c = ‚àÖ := rfl

lemma pairSet_subset : pairSet J a c ‚äÜ J √óÀ¢ J := by
  unfold pairSet
  rw [Finset.biUnion_subset_iff_forall_subset]
  intro i hi
  by_cases hJ : J.Nonempty
  ¬∑ simp only [pairSetSeq, hJ, ‚ÜìreduceDIte]
    apply Finset.product_subset_product
    ¬∑ exact Finset.singleton_subset_iff.mpr (point_mem_logSizeBallSeq_init hJ _)
    exact (Finset.filter_subset _ _).trans (finset_logSizeBallSeq_subset_logSizeBallSeq_init _ _)
  simp [pairSetSeq, hJ]

lemma card_pairSetSeq_le_logSizeRadius_mul (hJ : J.Nonempty) (i : ‚Ñï) (ha : 1 < a) :
    ‚Üë(#(pairSetSeq J a c i)) ‚â§ (if (logSizeBallSeq J hJ a c i).finset.Nonempty then 1 else 0)
    * a ^ (logSizeBallSeq J hJ a c i).radius := by
  induction i with
  | zero =>
    simpa [pairSetSeq, hJ, finset_logSizeBallSeq_zero, logSizeBallStruct.ball,
      radius_logSizeBallSeq_zero] using card_le_logSizeRadius_le_pow_logSizeRadius ha
  | succ i ih =>
    by_cases h : (logSizeBallSeq J hJ a c (i + 1)).finset.Nonempty
    ¬∑ simpa [pairSetSeq, logSizeBallStruct.ball, h, hJ]
        using card_le_logSizeRadius_le_pow_logSizeRadius ha
    simp [pairSetSeq, logSizeBallStruct.ball, Finset.not_nonempty_iff_eq_empty.mp h, hJ]

lemma logSizeRadius_le_card_smallBall (hJ : J.Nonempty) (i : ‚Ñï) (ha : 1 < a) :
    (if (logSizeBallSeq J hJ a c i).finset.Nonempty then 1 else 0) *
    a ^ ((logSizeBallSeq J hJ a c i).radius - 1) ‚â§ #((logSizeBallSeq J hJ a c i).smallBall c) := by
  match i with
  | 0 =>
    simpa [finset_logSizeBallSeq_zero, hJ, logSizeBallStruct.smallBall, radius_logSizeBallSeq_zero]
      using pow_logSizeRadius_le_card_le_logSizeRadius ha (Exists.choose_spec hJ)
  | i + 1 =>
    by_cases h : (logSizeBallSeq J hJ a c (i + 1)).finset.Nonempty
    ¬∑ simpa [h, logSizeBallStruct.smallBall, radius_logSizeBallSeq_add_one]
        using pow_logSizeRadius_le_card_le_logSizeRadius ha (point_mem_finset_logSizeBallSeq hJ _ h)
    simp [Finset.not_nonempty_iff_eq_empty.mp h]

lemma card_pairSet_le (ha : 1 < a) : #(pairSet J a c) ‚â§ a * #J := by
  wlog hJ : J.Nonempty
  ¬∑ simp [Finset.not_nonempty_iff_eq_empty.mp hJ]
  unfold pairSet
  grw [Finset.card_biUnion_le, Nat.cast_sum,
    Finset.sum_le_sum
      (fun i _ ‚Ü¶ card_pairSetSeq_le_logSizeRadius_mul hJ i ha),
    Finset.sum_le_sum
      (fun _ _ ‚Ü¶ mul_le_mul_left' (pow_le_pow_right‚ÇÄ (le_of_lt ha) (le_tsub_add)) _)]
  conv_lhs => enter [2]; ext _; rw [pow_add, pow_one, ‚Üê mul_assoc, mul_comm]
  grw [‚Üê Finset.mul_sum, mul_le_mul_left']
  grw [(Finset.sum_le_sum (fun i _ ‚Ü¶ logSizeRadius_le_card_smallBall hJ i ha)), ‚Üê Nat.cast_sum,
    Nat.cast_le,
    ‚Üê Finset.card_biUnion (fun _ _ _ _ hij ‚Ü¶ disjoint_smallBall_logSizeBallSeq hJ hij),
    Finset.card_le_card]
  unfold logSizeBallStruct.smallBall
  rw [Finset.biUnion_subset_iff_forall_subset]
  intro i _
  exact (Finset.filter_subset _ _).trans (finset_logSizeBallSeq_subset_logSizeBallSeq_init _ _)

lemma edist_le_of_mem_pairSet (ha : 1 < a) (hJ_card : #J ‚â§ a ^ n) {s t : T}
    (h : (s, t) ‚àà pairSet J a c) : edist s t ‚â§ n * c := by
  obtain ‚ü®i, hiJ, h'‚ü© : ‚àÉ i < #J, (s, t) ‚àà pairSetSeq J a c i := by simpa [pairSet] using h
  have hJ : J.Nonempty := Finset.card_pos.mp (Nat.zero_lt_of_lt hiJ)
  wlog hn : 1 ‚â§ n
  ¬∑ convert zero_le (n * c)
    convert edist_self _
    simp [Nat.lt_one_iff.mp (Nat.lt_of_not_ge hn)] at hJ_card
    have ‚ü®hs, ht‚ü© := Finset.mem_product.mp (pairSet_subset h)
    exact Finset.card_le_one_iff.mp hJ_card ht hs
  simp only [pairSetSeq, hJ, ‚ÜìreduceDIte, logSizeBallStruct.ball, Finset.product_eq_sprod,
    Finset.singleton_product, Finset.mem_map, Finset.mem_filter, Function.Embedding.coeFn_mk,
    Prod.mk.injEq, exists_eq_right_right] at h'
  obtain ‚ü®‚ü®ht, hdist‚ü©, rfl‚ü© := h'
  refine le_trans hdist (mul_le_mul_right' ?_ c)
  gcongr
  exact radius_logSizeBallSeq_le hJ ha hn hJ_card i

lemma iSup_edist_pairSet {E : Type*} [PseudoEMetricSpace E] (ha : 1 < a) (f : T ‚Üí E) :
    ‚®Ü (s : J) (t : { t : J // edist s t ‚â§ c}), edist (f s) (f t)
        ‚â§ 2 * ‚®Ü p : pairSet J a c, edist (f p.1.1) (f p.1.2) := by
  rw [iSup_le_iff]; rintro ‚ü®s, hs‚ü©
  rw [iSup_le_iff]; rintro ‚ü®‚ü®t, ht‚ü©, hst‚ü©
  have hJ : J.Nonempty := ‚ü®s, hs‚ü©
  let P (l : ‚Ñï) := s ‚àà (logSizeBallSeq J hJ a c l).finset ‚àß t ‚àà (logSizeBallSeq J hJ a c l).finset
  let l := Nat.findGreatest P (#J - 1)
  obtain ‚ü®hsV, htV‚ü© : P l := by
    apply Nat.findGreatest_spec (zero_le _)
    simp [P, finset_logSizeBallSeq_zero]
    exact ‚ü®hs, ht‚ü©
  wlog h : s ‚àâ (logSizeBallSeq J hJ a c (l + 1)).finset generalizing s t
  ¬∑ have h' : t ‚àâ (logSizeBallSeq J hJ a c (l + 1)).finset := by
      have hl : l < #J - 1 := by
        by_contra hl
        simp only [not_lt, tsub_le_iff_right] at hl
        have hlJ : l + 1 = #J := by
          refine Nat.le_antisymm_iff.mpr ‚ü®?_, hl‚ü©
          rw [‚Üê Nat.sub_add_cancel <| Order.one_le_iff_pos.mpr (Finset.card_pos.mpr hJ)]
          apply add_le_add_right (Nat.findGreatest_le _)
        apply h
        suffices h_emp : (logSizeBallSeq J hJ a c (l + 1)).finset = ‚àÖ from by simp [h_emp]
        rw [‚Üê Finset.card_eq_zero, ‚Üê Nat.le_zero, ‚Üê Nat.sub_self #J, hlJ]
        apply card_finset_logSizeBallSeq_le
      simp only [Decidable.not_not] at h
      have hP := Nat.findGreatest_is_greatest (lt_add_one l) (Nat.add_one_le_of_lt hl)
      simp [P, h] at hP; exact hP
    have hts : edist t s ‚â§ c := by rw [edist_comm]; exact hst
    rw [edist_comm]
    have hP : P = (fun l ‚Ü¶
      t ‚àà (logSizeBallSeq J hJ a c l).finset ‚àß s ‚àà (logSizeBallSeq J hJ a c l).finset) := by
        ext; simp [P, and_comm]
    simp [l, hP] at htV hsV h'
    exact this t ht s hs hts htV hsV h'
  simp only [finset_logSizeBallSeq_add_one, logSizeBallStruct.smallBall, Finset.mem_sdiff, hsV,
    Finset.mem_filter, true_and, not_le, not_lt] at h
  have hsB : s ‚àà (logSizeBallSeq J hJ a c l).ball c := by
    simp only [logSizeBallStruct.ball, Finset.mem_filter, hsV, true_and]
    apply le_trans h
    simp [mul_le_mul_right']
  have htB : t ‚àà (logSizeBallSeq J hJ a c l).ball c := by
    simp only [logSizeBallStruct.ball, Finset.mem_filter, htV, true_and]
    apply le_trans (edist_triangle _ s _)
    apply le_of_le_of_eq (add_le_add h hst)
    nth_rw 3 [‚Üê one_mul c]
    rw [‚Üê add_mul]
    congr
    rw [ENNReal.sub_add_eq_add_sub _ (ENNReal.one_ne_top),
      ENNReal.add_sub_cancel_right (ENNReal.one_ne_top)]
    rw [‚Üê Nat.cast_one]
    gcongr
    exact one_le_radius_logSizeBallSeq hJ ha l
  have hsP : ((logSizeBallSeq J hJ a c l).point, s) ‚àà pairSetSeq J a c l := by
    simp [pairSetSeq, hJ, hsB]
  have htP : ((logSizeBallSeq J hJ a c l).point, t) ‚àà pairSetSeq J a c l := by
    simp [pairSetSeq, hJ, htB]
  have sup_bound {x y : T} (hxy : (x,y) ‚àà pairSetSeq J a c l) :
    edist (f x) (f y) ‚â§  ‚®Ü p : pairSet J a c, edist (f p.1.1) (f p.1.2) := by
    simp only [iSup_subtype]
    apply le_iSup_of_le (i := (x,y))
    apply le_iSup_of_le
    ¬∑ exact le_rfl
    refine Finset.mem_biUnion.mpr ‚ü®l, ?_, hxy‚ü©
    refine Finset.mem_range.mpr <| lt_of_le_of_lt (Nat.findGreatest_le (#J - 1)) ?_
    exact Nat.sub_lt (Finset.card_pos.mpr hJ) zero_lt_one
  rw [two_mul]
  apply le_trans (edist_triangle _ (f (logSizeBallSeq J hJ a c l).point) _)
  rw [edist_comm]
  apply add_le_add (sup_bound hsP) (sup_bound htP)

end PairReduction

open PairReduction in
/-- **Pair Reduction**: Given pseudometric spaces `T` and `E`, `c ‚â• 0`, and a finite subset `J` of
`T` such that `|J| ‚â§ a‚Åø` for some `a ‚â• 0` and `n : ‚Ñï`, `pair_reduction` states that there exists a
set `K ‚äÜ J¬≤` such that for any function `f : T ‚Üí E`:
1. `|K| ‚â§ a|J|`
2. `‚àÄ (s, t) ‚àà K, d(s, t) ‚â§ cn`
3. `sup_{s, t ‚àà J : d(s, t) ‚â§ c} d(f(s), f(t)) ‚â§ 2 sup_{(s, t) ‚àà K} d(f(s), f(t))`
-/
theorem EMetric.pair_reduction
    (hJ_card : #J ‚â§ a ^ n) (c : ‚Ñù‚â•0‚àû) (E : Type*) [PseudoEMetricSpace E] :
    ‚àÉ K : Finset (T √ó T), K ‚äÜ J √óÀ¢ J
      ‚àß #K ‚â§ a * #J
      ‚àß (‚àÄ s t, (s, t) ‚àà K ‚Üí edist s t ‚â§ n * c)
      ‚àß (‚àÄ f : T ‚Üí E,
        ‚®Ü (s : J) (t : { t : J // edist s t ‚â§ c}), edist (f s) (f t)
        ‚â§ 2 * ‚®Ü p : K, edist (f p.1.1) (f p.1.2)) := by
  classical
  rcases le_or_gt a 1 with ha1 | ha1
  ¬∑ rcases isEmpty_or_nonempty J with hJ | hJ
    ¬∑ simp only [Finset.isEmpty_coe_sort] at hJ
      simp [hJ]
    obtain ‚ü®x‚ÇÄ, rfl‚ü© : ‚àÉ x‚ÇÄ, J = {x‚ÇÄ} := by
      rw [‚Üê Finset.card_eq_one]
      refine le_antisymm ?_ ?_
      ¬∑ suffices (#J : ENNReal) ‚â§ 1 by norm_cast at this
        refine hJ_card.trans ?_
        conv_rhs => rw [‚Üê one_pow n]
        exact ENNReal.pow_le_pow_left ha1
      ¬∑ rwa [Finset.one_le_card, ‚Üê Finset.nonempty_coe_sort]
    simp_all
  ¬∑ exact ‚ü®pairSet J a c, pairSet_subset, card_pairSet_le ha1,
      fun _ _ ‚Ü¶ edist_le_of_mem_pairSet ha1 hJ_card, iSup_edist_pairSet ha1‚ü©
