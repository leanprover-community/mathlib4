/-
Copyright (c) 2018 Rohan Mitta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Rohan Mitta, Kevin Buzzard, Alistair Tucker, Johannes H√∂lzl, Yury Kudryashov, Winston Yin
-/
import Mathlib.Algebra.Group.End
import Mathlib.Tactic.Finiteness
import Mathlib.Topology.EMetricSpace.Diam

/-!
# Lipschitz continuous functions

A map `f : Œ± ‚Üí Œ≤` between two (extended) metric spaces is called *Lipschitz continuous*
with constant `K ‚â• 0` if for all `x, y` we have `edist (f x) (f y) ‚â§ K * edist x y`.
For a metric space, the latter inequality is equivalent to `dist (f x) (f y) ‚â§ K * dist x y`.
There is also a version asserting this inequality only for `x` and `y` in some set `s`.
Finally, `f : Œ± ‚Üí Œ≤` is called *locally Lipschitz continuous* if each `x : Œ±` has a neighbourhood
on which `f` is Lipschitz continuous (with some constant).

In this file we provide various ways to prove that various combinations of Lipschitz continuous
functions are Lipschitz continuous. We also prove that Lipschitz continuous functions are
uniformly continuous, and that locally Lipschitz functions are continuous.

## Main definitions and lemmas

* `LipschitzWith K f`: states that `f` is Lipschitz with constant `K : ‚Ñù‚â•0`
* `LipschitzOnWith K f s`: states that `f` is Lipschitz with constant `K : ‚Ñù‚â•0` on a set `s`
* `LipschitzWith.uniformContinuous`: a Lipschitz function is uniformly continuous
* `LipschitzOnWith.uniformContinuousOn`: a function which is Lipschitz on a set `s` is uniformly
  continuous on `s`.
* `LocallyLipschitz f`: states that `f` is locally Lipschitz
* `LocallyLipschitzOn f s`: states that `f` is locally Lipschitz on `s`.
* `LocallyLipschitz.continuous`: a locally Lipschitz function is continuous.


## Implementation notes

The parameter `K` has type `‚Ñù‚â•0`. This way we avoid conjunction in the definition and have
coercions both to `‚Ñù` and `‚Ñù‚â•0‚àû`. Constructors whose names end with `'` take `K : ‚Ñù` as an
argument, and return `LipschitzWith (Real.toNNReal K) f`.
-/

universe u v w x

open Filter Function Set Topology NNReal ENNReal Bornology

variable {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w} {Œπ : Type x}

section PseudoEMetricSpace
variable [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] {K : ‚Ñù‚â•0} {s t : Set Œ±} {f : Œ± ‚Üí Œ≤}

/-- A function `f` is **Lipschitz continuous** with constant `K ‚â• 0` if for all `x, y`
we have `dist (f x) (f y) ‚â§ K * dist x y`. -/
def LipschitzWith (K : ‚Ñù‚â•0) (f : Œ± ‚Üí Œ≤) := ‚àÄ x y, edist (f x) (f y) ‚â§ K * edist x y

/-- A function `f` is **Lipschitz continuous** with constant `K ‚â• 0` **on `s`** if
for all `x, y` in `s` we have `dist (f x) (f y) ‚â§ K * dist x y`. -/
def LipschitzOnWith (K : ‚Ñù‚â•0) (f : Œ± ‚Üí Œ≤) (s : Set Œ±) :=
  ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí ‚àÄ ‚¶Éy‚¶Ñ, y ‚àà s ‚Üí edist (f x) (f y) ‚â§ K * edist x y

/-- `f : Œ± ‚Üí Œ≤` is called **locally Lipschitz continuous** iff every point `x`
has a neighbourhood on which `f` is Lipschitz. -/
def LocallyLipschitz (f : Œ± ‚Üí Œ≤) : Prop := ‚àÄ x, ‚àÉ K, ‚àÉ t ‚àà ùìù x, LipschitzOnWith K f t

/-- `f : Œ± ‚Üí Œ≤` is called **locally Lipschitz continuous** on `s` iff every point `x` of `s`
has a neighbourhood within `s` on which `f` is Lipschitz. -/
def LocallyLipschitzOn (s : Set Œ±) (f : Œ± ‚Üí Œ≤) : Prop :=
  ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí ‚àÉ K, ‚àÉ t ‚àà ùìù[s] x, LipschitzOnWith K f t

/-- Every function is Lipschitz on the empty set (with any Lipschitz constant). -/
@[simp]
theorem lipschitzOnWith_empty (K : ‚Ñù‚â•0) (f : Œ± ‚Üí Œ≤) : LipschitzOnWith K f ‚àÖ := fun _ => False.elim

@[simp] lemma locallyLipschitzOn_empty (f : Œ± ‚Üí Œ≤) : LocallyLipschitzOn ‚àÖ f := fun _ ‚Ü¶ False.elim

/-- Being Lipschitz on a set is monotone w.r.t. that set. -/
theorem LipschitzOnWith.mono (hf : LipschitzOnWith K f t) (h : s ‚äÜ t) : LipschitzOnWith K f s :=
  fun _x x_in _y y_in => hf (h x_in) (h y_in)

lemma LocallyLipschitzOn.mono (hf : LocallyLipschitzOn t f) (h : s ‚äÜ t) : LocallyLipschitzOn s f :=
  fun x hx ‚Ü¶ by obtain ‚ü®K, u, hu, hfu‚ü© := hf (h hx); exact ‚ü®K, u, nhdsWithin_mono _ h hu, hfu‚ü©

/-- `f` is Lipschitz iff it is Lipschitz on the entire space. -/
@[simp] lemma lipschitzOnWith_univ : LipschitzOnWith K f univ ‚Üî LipschitzWith K f := by
  simp [LipschitzOnWith, LipschitzWith]

@[simp] lemma locallyLipschitzOn_univ : LocallyLipschitzOn univ f ‚Üî LocallyLipschitz f := by
  simp [LocallyLipschitzOn, LocallyLipschitz]

protected lemma LocallyLipschitz.locallyLipschitzOn (h : LocallyLipschitz f) :
    LocallyLipschitzOn s f := (locallyLipschitzOn_univ.2 h).mono s.subset_univ

theorem lipschitzOnWith_iff_restrict : LipschitzOnWith K f s ‚Üî LipschitzWith K (s.restrict f) := by
  simp [LipschitzOnWith, LipschitzWith]

lemma lipschitzOnWith_restrict {t : Set s} :
    LipschitzOnWith K (s.restrict f) t ‚Üî LipschitzOnWith K f (s ‚à© Subtype.val '' t) := by
  simp [LipschitzOnWith]

lemma locallyLipschitzOn_iff_restrict :
    LocallyLipschitzOn s f ‚Üî LocallyLipschitz (s.restrict f) := by
  simp only [LocallyLipschitzOn, LocallyLipschitz, SetCoe.forall',
    lipschitzOnWith_restrict,
    nhds_subtype_eq_comap_nhdsWithin, mem_comap]
  congr! with x K
  constructor
  ¬∑ rintro ‚ü®t, ht, hft‚ü©
    exact ‚ü®_, ‚ü®t, ht, Subset.rfl‚ü©, hft.mono <| inter_subset_right.trans <| image_preimage_subset ..‚ü©
  ¬∑ rintro ‚ü®t, ‚ü®u, hu, hut‚ü©, hft‚ü©
    exact ‚ü®s ‚à© u, Filter.inter_mem self_mem_nhdsWithin hu,
      hft.mono fun x hx ‚Ü¶ ‚ü®hx.1, ‚ü®x, hx.1‚ü©, hut hx.2, rfl‚ü©‚ü©

alias ‚ü®LipschitzOnWith.to_restrict, _‚ü© := lipschitzOnWith_iff_restrict
alias ‚ü®LocallyLipschitzOn.restrict, _‚ü© := locallyLipschitzOn_iff_restrict

lemma Set.MapsTo.lipschitzOnWith_iff_restrict {t : Set Œ≤} (h : MapsTo f s t) :
    LipschitzOnWith K f s ‚Üî LipschitzWith K (h.restrict f s t) :=
  _root_.lipschitzOnWith_iff_restrict

alias ‚ü®LipschitzOnWith.to_restrict_mapsTo, _‚ü© := Set.MapsTo.lipschitzOnWith_iff_restrict

end PseudoEMetricSpace

namespace LipschitzWith

open EMetric

variable [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] [PseudoEMetricSpace Œ≥]
variable {K : ‚Ñù‚â•0} {f : Œ± ‚Üí Œ≤} {x y : Œ±} {r : ‚Ñù‚â•0‚àû} {s : Set Œ±}

protected theorem lipschitzOnWith (h : LipschitzWith K f) : LipschitzOnWith K f s :=
  fun x _ y _ => h x y

theorem edist_le_mul (h : LipschitzWith K f) (x y : Œ±) : edist (f x) (f y) ‚â§ K * edist x y :=
  h x y

theorem edist_le_mul_of_le (h : LipschitzWith K f) (hr : edist x y ‚â§ r) :
    edist (f x) (f y) ‚â§ K * r :=
  (h x y).trans <| mul_left_mono hr

theorem edist_lt_mul_of_lt (h : LipschitzWith K f) (hK : K ‚â† 0) (hr : edist x y < r) :
    edist (f x) (f y) < K * r :=
  (h x y).trans_lt <| (ENNReal.mul_lt_mul_left (ENNReal.coe_ne_zero.2 hK) ENNReal.coe_ne_top).2 hr

theorem mapsTo_emetric_closedBall (h : LipschitzWith K f) (x : Œ±) (r : ‚Ñù‚â•0‚àû) :
    MapsTo f (closedBall x r) (closedBall (f x) (K * r)) := fun _y hy => h.edist_le_mul_of_le hy

theorem mapsTo_emetric_ball (h : LipschitzWith K f) (hK : K ‚â† 0) (x : Œ±) (r : ‚Ñù‚â•0‚àû) :
    MapsTo f (ball x r) (ball (f x) (K * r)) := fun _y hy => h.edist_lt_mul_of_lt hK hy

theorem edist_lt_top (hf : LipschitzWith K f) {x y : Œ±} (h : edist x y ‚â† ‚ä§) :
    edist (f x) (f y) < ‚ä§ :=
  (hf x y).trans_lt (by finiteness)

theorem mul_edist_le (h : LipschitzWith K f) (x y : Œ±) :
    (K‚Åª¬π : ‚Ñù‚â•0‚àû) * edist (f x) (f y) ‚â§ edist x y := by
  rw [mul_comm, ‚Üê div_eq_mul_inv]
  exact ENNReal.div_le_of_le_mul' (h x y)

protected theorem of_edist_le (h : ‚àÄ x y, edist (f x) (f y) ‚â§ edist x y) : LipschitzWith 1 f :=
  fun x y => by simp only [ENNReal.coe_one, one_mul, h]

protected theorem weaken (hf : LipschitzWith K f) {K' : ‚Ñù‚â•0} (h : K ‚â§ K') : LipschitzWith K' f :=
  fun x y => le_trans (hf x y) <| mul_right_mono (ENNReal.coe_le_coe.2 h)

theorem ediam_image_le (hf : LipschitzWith K f) (s : Set Œ±) :
    EMetric.diam (f '' s) ‚â§ K * EMetric.diam s := by
  apply EMetric.diam_le
  rintro _ ‚ü®x, hx, rfl‚ü© _ ‚ü®y, hy, rfl‚ü©
  exact hf.edist_le_mul_of_le (EMetric.edist_le_diam_of_mem hx hy)

theorem edist_lt_of_edist_lt_div (hf : LipschitzWith K f) {x y : Œ±} {d : ‚Ñù‚â•0‚àû}
    (h : edist x y < d / K) : edist (f x) (f y) < d :=
  calc
    edist (f x) (f y) ‚â§ K * edist x y := hf x y
    _ < d := ENNReal.mul_lt_of_lt_div' h

/-- A Lipschitz function is uniformly continuous. -/
protected theorem uniformContinuous (hf : LipschitzWith K f) : UniformContinuous f :=
  EMetric.uniformContinuous_iff.2 fun Œµ Œµpos =>
    ‚ü®Œµ / K, ENNReal.div_pos_iff.2 ‚ü®ne_of_gt Œµpos, ENNReal.coe_ne_top‚ü©, hf.edist_lt_of_edist_lt_div‚ü©

/-- A Lipschitz function is continuous. -/
protected theorem continuous (hf : LipschitzWith K f) : Continuous f :=
  hf.uniformContinuous.continuous

/-- Constant functions are Lipschitz (with any constant). -/
protected theorem const (b : Œ≤) : LipschitzWith 0 fun _ : Œ± => b := fun x y => by
  simp only [edist_self, zero_le]

protected theorem const' (b : Œ≤) {K : ‚Ñù‚â•0} : LipschitzWith K fun _ : Œ± => b := fun x y => by
  simp only [edist_self, zero_le]

/-- The identity is 1-Lipschitz. -/
protected theorem id : LipschitzWith 1 (@id Œ±) :=
  LipschitzWith.of_edist_le fun _ _ => le_rfl

/-- The inclusion of a subset is 1-Lipschitz. -/
protected theorem subtype_val (s : Set Œ±) : LipschitzWith 1 (Subtype.val : s ‚Üí Œ±) :=
  LipschitzWith.of_edist_le fun _ _ => le_rfl

theorem subtype_mk (hf : LipschitzWith K f) {p : Œ≤ ‚Üí Prop} (hp : ‚àÄ x, p (f x)) :
    LipschitzWith K (fun x => ‚ü®f x, hp x‚ü© : Œ± ‚Üí { y // p y }) :=
  hf

protected theorem eval {Œ± : Œπ ‚Üí Type u} [‚àÄ i, PseudoEMetricSpace (Œ± i)] [Fintype Œπ] (i : Œπ) :
    LipschitzWith 1 (Function.eval i : (‚àÄ i, Œ± i) ‚Üí Œ± i) :=
  LipschitzWith.of_edist_le fun f g => by convert edist_le_pi_edist f g i

/-- The restriction of a `K`-Lipschitz function is `K`-Lipschitz. -/
protected theorem restrict (hf : LipschitzWith K f) (s : Set Œ±) : LipschitzWith K (s.restrict f) :=
  fun x y => hf x y

/-- The composition of Lipschitz functions is Lipschitz. -/
protected theorem comp {Kf Kg : ‚Ñù‚â•0} {f : Œ≤ ‚Üí Œ≥} {g : Œ± ‚Üí Œ≤} (hf : LipschitzWith Kf f)
    (hg : LipschitzWith Kg g) : LipschitzWith (Kf * Kg) (f ‚àò g) := fun x y =>
  calc
    edist (f (g x)) (f (g y)) ‚â§ Kf * edist (g x) (g y) := hf _ _
    _ ‚â§ Kf * (Kg * edist x y) := mul_left_mono (hg _ _)
    _ = (Kf * Kg : ‚Ñù‚â•0) * edist x y := by rw [‚Üê mul_assoc, ENNReal.coe_mul]

theorem comp_lipschitzOnWith {Kf Kg : ‚Ñù‚â•0} {f : Œ≤ ‚Üí Œ≥} {g : Œ± ‚Üí Œ≤} {s : Set Œ±}
    (hf : LipschitzWith Kf f) (hg : LipschitzOnWith Kg g s) : LipschitzOnWith (Kf * Kg) (f ‚àò g) s :=
  lipschitzOnWith_iff_restrict.mpr <| hf.comp hg.to_restrict

protected theorem prod_fst : LipschitzWith 1 (@Prod.fst Œ± Œ≤) :=
  LipschitzWith.of_edist_le fun _ _ => le_max_left _ _

protected theorem prod_snd : LipschitzWith 1 (@Prod.snd Œ± Œ≤) :=
  LipschitzWith.of_edist_le fun _ _ => le_max_right _ _

/-- If `f` and `g` are Lipschitz functions, so is the induced map `f √ó g` to the product type. -/
protected theorem prodMk {f : Œ± ‚Üí Œ≤} {Kf : ‚Ñù‚â•0} (hf : LipschitzWith Kf f) {g : Œ± ‚Üí Œ≥} {Kg : ‚Ñù‚â•0}
    (hg : LipschitzWith Kg g) : LipschitzWith (max Kf Kg) fun x => (f x, g x) := by
  intro x y
  rw [ENNReal.coe_mono.map_max, Prod.edist_eq, max_mul]
  exact max_le_max (hf x y) (hg x y)

@[deprecated (since := "2025-03-10")]
protected alias prod := LipschitzWith.prodMk

protected theorem prodMk_left (a : Œ±) : LipschitzWith 1 (Prod.mk a : Œ≤ ‚Üí Œ± √ó Œ≤) := by
  simpa only [max_eq_right zero_le_one] using (LipschitzWith.const a).prodMk LipschitzWith.id

@[deprecated (since := "2025-03-10")]
protected alias prod_mk_left := LipschitzWith.prodMk_left

protected theorem prodMk_right (b : Œ≤) : LipschitzWith 1 fun a : Œ± => (a, b) := by
  simpa only [max_eq_left zero_le_one] using LipschitzWith.id.prodMk (LipschitzWith.const b)

@[deprecated (since := "2025-03-10")]
protected alias prod_mk_right := LipschitzWith.prodMk_right

protected theorem uncurry {f : Œ± ‚Üí Œ≤ ‚Üí Œ≥} {KŒ± KŒ≤ : ‚Ñù‚â•0} (hŒ± : ‚àÄ b, LipschitzWith KŒ± fun a => f a b)
    (hŒ≤ : ‚àÄ a, LipschitzWith KŒ≤ (f a)) : LipschitzWith (KŒ± + KŒ≤) (Function.uncurry f) := by
  rintro ‚ü®a‚ÇÅ, b‚ÇÅ‚ü© ‚ü®a‚ÇÇ, b‚ÇÇ‚ü©
  simp only [Function.uncurry, ENNReal.coe_add, add_mul]
  apply le_trans (edist_triangle _ (f a‚ÇÇ b‚ÇÅ) _)
  exact
    add_le_add (le_trans (hŒ± _ _ _) <| mul_left_mono <| le_max_left _ _)
      (le_trans (hŒ≤ _ _ _) <| mul_left_mono <| le_max_right _ _)

/-- Iterates of a Lipschitz function are Lipschitz. -/
protected theorem iterate {f : Œ± ‚Üí Œ±} (hf : LipschitzWith K f) : ‚àÄ n, LipschitzWith (K ^ n) f^[n]
  | 0 => by simpa only [pow_zero] using LipschitzWith.id
  | n + 1 => by rw [pow_succ]; exact (LipschitzWith.iterate hf n).comp hf

theorem edist_iterate_succ_le_geometric {f : Œ± ‚Üí Œ±} (hf : LipschitzWith K f) (x n) :
    edist (f^[n] x) (f^[n+1] x) ‚â§ edist x (f x) * (K : ‚Ñù‚â•0‚àû) ^ n := by
  rw [iterate_succ, mul_comm]
  simpa only [ENNReal.coe_pow] using (hf.iterate n) x (f x)

protected theorem mul_end {f g : Function.End Œ±} {Kf Kg} (hf : LipschitzWith Kf f)
    (hg : LipschitzWith Kg g) : LipschitzWith (Kf * Kg) (f * g : Function.End Œ±) :=
  hf.comp hg

/-- The product of a list of Lipschitz continuous endomorphisms is a Lipschitz continuous
endomorphism. -/
protected theorem list_prod (f : Œπ ‚Üí Function.End Œ±) (K : Œπ ‚Üí ‚Ñù‚â•0)
    (h : ‚àÄ i, LipschitzWith (K i) (f i)) : ‚àÄ l : List Œπ, LipschitzWith (l.map K).prod (l.map f).prod
  | [] => by simpa using LipschitzWith.id
  | i::l => by
    simp only [List.map_cons, List.prod_cons]
    exact (h i).mul_end (LipschitzWith.list_prod f K h l)

protected theorem pow_end {f : Function.End Œ±} {K} (h : LipschitzWith K f) :
    ‚àÄ n : ‚Ñï, LipschitzWith (K ^ n) (f ^ n : Function.End Œ±)
  | 0 => by simpa only [pow_zero] using LipschitzWith.id
  | n + 1 => by
    rw [pow_succ, pow_succ]
    exact (LipschitzWith.pow_end h n).mul_end h

end LipschitzWith

namespace LipschitzOnWith

variable [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] [PseudoEMetricSpace Œ≥]
variable {K : ‚Ñù‚â•0} {s : Set Œ±} {f : Œ± ‚Üí Œ≤}

protected theorem uniformContinuousOn (hf : LipschitzOnWith K f s) : UniformContinuousOn f s :=
  uniformContinuousOn_iff_restrict.mpr hf.to_restrict.uniformContinuous

protected theorem continuousOn (hf : LipschitzOnWith K f s) : ContinuousOn f s :=
  hf.uniformContinuousOn.continuousOn

theorem edist_le_mul_of_le (h : LipschitzOnWith K f s) {x y : Œ±} (hx : x ‚àà s) (hy : y ‚àà s)
    {r : ‚Ñù‚â•0‚àû} (hr : edist x y ‚â§ r) :
    edist (f x) (f y) ‚â§ K * r :=
  (h hx hy).trans <| mul_left_mono hr

theorem edist_lt_of_edist_lt_div (hf : LipschitzOnWith K f s) {x y : Œ±} (hx : x ‚àà s) (hy : y ‚àà s)
    {d : ‚Ñù‚â•0‚àû} (hd : edist x y < d / K) : edist (f x) (f y) < d :=
  hf.to_restrict.edist_lt_of_edist_lt_div <| show edist (‚ü®x, hx‚ü© : s) ‚ü®y, hy‚ü© < d / K from hd

protected theorem comp {g : Œ≤ ‚Üí Œ≥} {t : Set Œ≤} {Kg : ‚Ñù‚â•0} (hg : LipschitzOnWith Kg g t)
    (hf : LipschitzOnWith K f s) (hmaps : MapsTo f s t) : LipschitzOnWith (Kg * K) (g ‚àò f) s :=
  lipschitzOnWith_iff_restrict.mpr <| hg.to_restrict.comp (hf.to_restrict_mapsTo hmaps)

/-- If `f` and `g` are Lipschitz on `s`, so is the induced map `f √ó g` to the product type. -/
protected theorem prodMk {g : Œ± ‚Üí Œ≥} {Kf Kg : ‚Ñù‚â•0} (hf : LipschitzOnWith Kf f s)
    (hg : LipschitzOnWith Kg g s) : LipschitzOnWith (max Kf Kg) (fun x => (f x, g x)) s := by
  intro _ hx _ hy
  rw [ENNReal.coe_mono.map_max, Prod.edist_eq, max_mul]
  exact max_le_max (hf hx hy) (hg hx hy)

@[deprecated (since := "2025-03-10")]
protected alias prod := LipschitzOnWith.prodMk

theorem ediam_image2_le (f : Œ± ‚Üí Œ≤ ‚Üí Œ≥) {K‚ÇÅ K‚ÇÇ : ‚Ñù‚â•0} (s : Set Œ±) (t : Set Œ≤)
    (hf‚ÇÅ : ‚àÄ b ‚àà t, LipschitzOnWith K‚ÇÅ (f ¬∑ b) s) (hf‚ÇÇ : ‚àÄ a ‚àà s, LipschitzOnWith K‚ÇÇ (f a) t) :
    EMetric.diam (Set.image2 f s t) ‚â§ ‚ÜëK‚ÇÅ * EMetric.diam s + ‚ÜëK‚ÇÇ * EMetric.diam t := by
  simp only [EMetric.diam_le_iff, forall_mem_image2]
  intro a‚ÇÅ ha‚ÇÅ b‚ÇÅ hb‚ÇÅ a‚ÇÇ ha‚ÇÇ b‚ÇÇ hb‚ÇÇ
  refine (edist_triangle _ (f a‚ÇÇ b‚ÇÅ) _).trans ?_
  exact
    add_le_add
      ((hf‚ÇÅ b‚ÇÅ hb‚ÇÅ ha‚ÇÅ ha‚ÇÇ).trans <| mul_left_mono <| EMetric.edist_le_diam_of_mem ha‚ÇÅ ha‚ÇÇ)
      ((hf‚ÇÇ a‚ÇÇ ha‚ÇÇ hb‚ÇÅ hb‚ÇÇ).trans <| mul_left_mono <| EMetric.edist_le_diam_of_mem hb‚ÇÅ hb‚ÇÇ)

end LipschitzOnWith

namespace LocallyLipschitz
variable [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] [PseudoEMetricSpace Œ≥] {f : Œ± ‚Üí Œ≤}

/-- A Lipschitz function is locally Lipschitz. -/
protected lemma _root_.LipschitzWith.locallyLipschitz {K : ‚Ñù‚â•0} (hf : LipschitzWith K f) :
    LocallyLipschitz f :=
  fun _ ‚Ü¶ ‚ü®K, univ, Filter.univ_mem, lipschitzOnWith_univ.mpr hf‚ü©

/-- The identity function is locally Lipschitz. -/
protected lemma id : LocallyLipschitz (@id Œ±) := LipschitzWith.id.locallyLipschitz

/-- Constant functions are locally Lipschitz. -/
protected lemma const (b : Œ≤) : LocallyLipschitz (fun _ : Œ± ‚Ü¶ b) :=
  (LipschitzWith.const b).locallyLipschitz

/-- A locally Lipschitz function is continuous. (The converse is false: for example,
$x ‚Ü¶ \sqrt{x}$ is continuous, but not locally Lipschitz at 0.) -/
protected theorem continuous {f : Œ± ‚Üí Œ≤} (hf : LocallyLipschitz f) : Continuous f := by
  rw [continuous_iff_continuousAt]
  intro x
  rcases (hf x) with ‚ü®K, t, ht, hK‚ü©
  exact (hK.continuousOn).continuousAt ht

/-- The composition of locally Lipschitz functions is locally Lipschitz. -/
protected lemma comp {f : Œ≤ ‚Üí Œ≥} {g : Œ± ‚Üí Œ≤}
    (hf : LocallyLipschitz f) (hg : LocallyLipschitz g) : LocallyLipschitz (f ‚àò g) := by
  intro x
  -- g is Lipschitz on t ‚àã x, f is Lipschitz on u ‚àã g(x)
  rcases hg x with ‚ü®Kg, t, ht, hgL‚ü©
  rcases hf (g x) with ‚ü®Kf, u, hu, hfL‚ü©
  refine ‚ü®Kf * Kg, t ‚à© g‚Åª¬π' u, inter_mem ht (hg.continuous.continuousAt hu), ?_‚ü©
  exact hfL.comp (hgL.mono inter_subset_left)
    ((mapsTo_preimage g u).mono_left inter_subset_right)

/-- If `f` and `g` are locally Lipschitz, so is the induced map `f √ó g` to the product type. -/
protected lemma prodMk {f : Œ± ‚Üí Œ≤} (hf : LocallyLipschitz f) {g : Œ± ‚Üí Œ≥} (hg : LocallyLipschitz g) :
    LocallyLipschitz fun x => (f x, g x) := by
  intro x
  rcases hf x with ‚ü®Kf, t‚ÇÅ, h‚ÇÅt, hfL‚ü©
  rcases hg x with ‚ü®Kg, t‚ÇÇ, h‚ÇÇt, hgL‚ü©
  refine ‚ü®max Kf Kg, t‚ÇÅ ‚à© t‚ÇÇ, Filter.inter_mem h‚ÇÅt h‚ÇÇt, ?_‚ü©
  exact (hfL.mono inter_subset_left).prodMk (hgL.mono inter_subset_right)

@[deprecated (since := "2025-03-10")]
protected alias prod := LocallyLipschitz.prodMk

protected theorem prodMk_left (a : Œ±) : LocallyLipschitz (Prod.mk a : Œ≤ ‚Üí Œ± √ó Œ≤) :=
  (LipschitzWith.prodMk_left a).locallyLipschitz

@[deprecated (since := "2025-03-10")]
protected alias prod_mk_left := LocallyLipschitz.prodMk_left

protected theorem prodMk_right (b : Œ≤) : LocallyLipschitz (fun a : Œ± => (a, b)) :=
  (LipschitzWith.prodMk_right b).locallyLipschitz

@[deprecated (since := "2025-03-10")]
protected alias prod_mk_right := LocallyLipschitz.prodMk_right

protected theorem iterate {f : Œ± ‚Üí Œ±} (hf : LocallyLipschitz f) : ‚àÄ n, LocallyLipschitz f^[n]
  | 0 => by simpa only [pow_zero] using LocallyLipschitz.id
  | n + 1 => by rw [iterate_add, iterate_one]; exact (hf.iterate n).comp hf

protected theorem mul_end {f g : Function.End Œ±} (hf : LocallyLipschitz f)
    (hg : LocallyLipschitz g) : LocallyLipschitz (f * g : Function.End Œ±) := hf.comp hg

protected theorem pow_end {f : Function.End Œ±} (h : LocallyLipschitz f) :
    ‚àÄ n : ‚Ñï, LocallyLipschitz (f ^ n : Function.End Œ±)
  | 0 => by simpa only [pow_zero] using LocallyLipschitz.id
  | n + 1 => by
    rw [pow_succ]
    exact (h.pow_end n).mul_end h

end LocallyLipschitz

namespace LocallyLipschitzOn
variable [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ±}

protected lemma continuousOn (hf : LocallyLipschitzOn s f) : ContinuousOn f s :=
  continuousOn_iff_continuous_restrict.2 hf.restrict.continuous

end LocallyLipschitzOn

/-- Consider a function `f : Œ± √ó Œ≤ ‚Üí Œ≥`. Suppose that it is continuous on each ‚Äúvertical fiber‚Äù
`{a} √ó t`, `a ‚àà s`, and is Lipschitz continuous on each ‚Äúhorizontal fiber‚Äù `s √ó {b}`, `b ‚àà t`
with the same Lipschitz constant `K`. Then it is continuous on `s √ó t`. Moreover, it suffices
to require continuity on vertical fibers for `a` from a subset `s' ‚äÜ s` that is dense in `s`.

The actual statement uses (Lipschitz) continuity of `fun y ‚Ü¶ f (a, y)` and `fun x ‚Ü¶ f (x, b)`
instead of continuity of `f` on subsets of the product space. -/
theorem continuousOn_prod_of_subset_closure_continuousOn_lipschitzOnWith [PseudoEMetricSpace Œ±]
    [TopologicalSpace Œ≤] [PseudoEMetricSpace Œ≥] (f : Œ± √ó Œ≤ ‚Üí Œ≥) {s s' : Set Œ±} {t : Set Œ≤}
    (hs' : s' ‚äÜ s) (hss' : s ‚äÜ closure s') (K : ‚Ñù‚â•0)
    (ha : ‚àÄ a ‚àà s', ContinuousOn (fun y => f (a, y)) t)
    (hb : ‚àÄ b ‚àà t, LipschitzOnWith K (fun x => f (x, b)) s) : ContinuousOn f (s √óÀ¢ t) := by
  rintro ‚ü®x, y‚ü© ‚ü®hx : x ‚àà s, hy : y ‚àà t‚ü©
  refine EMetric.nhds_basis_closed_eball.tendsto_right_iff.2 fun Œµ (Œµ0 : 0 < Œµ) => ?_
  replace Œµ0 : 0 < Œµ / 2 := ENNReal.half_pos Œµ0.ne'
  obtain ‚ü®Œ¥, Œ¥pos, hŒ¥‚ü© : ‚àÉ Œ¥ : ‚Ñù‚â•0, 0 < Œ¥ ‚àß (Œ¥ : ‚Ñù‚â•0‚àû) * ‚Üë(3 * K) < Œµ / 2 :=
    ENNReal.exists_nnreal_pos_mul_lt ENNReal.coe_ne_top Œµ0.ne'
  rw [‚Üê ENNReal.coe_pos] at Œ¥pos
  rcases EMetric.mem_closure_iff.1 (hss' hx) Œ¥ Œ¥pos with ‚ü®x', hx', hxx'‚ü©
  have A : s ‚à© EMetric.ball x Œ¥ ‚àà ùìù[s] x :=
    inter_mem_nhdsWithin _ (EMetric.ball_mem_nhds _ Œ¥pos)
  have B : t ‚à© { b | edist (f (x', b)) (f (x', y)) ‚â§ Œµ / 2 } ‚àà ùìù[t] y :=
    inter_mem self_mem_nhdsWithin (ha x' hx' y hy (EMetric.closedBall_mem_nhds (f (x', y)) Œµ0))
  filter_upwards [nhdsWithin_prod A B] with ‚ü®a, b‚ü© ‚ü®‚ü®has, hax‚ü©, ‚ü®hbt, hby‚ü©‚ü©
  calc
    edist (f (a, b)) (f (x, y)) ‚â§ edist (f (a, b)) (f (x', b)) + edist (f (x', b)) (f (x', y)) +
        edist (f (x', y)) (f (x, y)) := edist_triangle4 _ _ _ _
    _ ‚â§ K * (Œ¥ + Œ¥) + Œµ / 2 + K * Œ¥ := by
      gcongr
      ¬∑ refine (hb b hbt).edist_le_mul_of_le has (hs' hx') ?_
        exact (edist_triangle _ _ _).trans (add_le_add (le_of_lt hax) hxx'.le)
      ¬∑ exact hby
      ¬∑ exact (hb y hy).edist_le_mul_of_le (hs' hx') hx ((edist_comm _ _).trans_le hxx'.le)
    _ = Œ¥ * ‚Üë(3 * K) + Œµ / 2 := by push_cast; ring
    _ ‚â§ Œµ / 2 + Œµ / 2 := by gcongr
    _ = Œµ := ENNReal.add_halves _

/-- Consider a function `f : Œ± √ó Œ≤ ‚Üí Œ≥`. Suppose that it is continuous on each ‚Äúvertical fiber‚Äù
`{a} √ó t`, `a ‚àà s`, and is Lipschitz continuous on each ‚Äúhorizontal fiber‚Äù `s √ó {b}`, `b ‚àà t`
with the same Lipschitz constant `K`. Then it is continuous on `s √ó t`.

The actual statement uses (Lipschitz) continuity of `fun y ‚Ü¶ f (a, y)` and `fun x ‚Ü¶ f (x, b)`
instead of continuity of `f` on subsets of the product space. -/
theorem continuousOn_prod_of_continuousOn_lipschitzOnWith [PseudoEMetricSpace Œ±]
    [TopologicalSpace Œ≤] [PseudoEMetricSpace Œ≥] (f : Œ± √ó Œ≤ ‚Üí Œ≥) {s : Set Œ±} {t : Set Œ≤} (K : ‚Ñù‚â•0)
    (ha : ‚àÄ a ‚àà s, ContinuousOn (fun y => f (a, y)) t)
    (hb : ‚àÄ b ‚àà t, LipschitzOnWith K (fun x => f (x, b)) s) : ContinuousOn f (s √óÀ¢ t) :=
  continuousOn_prod_of_subset_closure_continuousOn_lipschitzOnWith
    f Subset.rfl subset_closure K ha hb

/-- Consider a function `f : Œ± √ó Œ≤ ‚Üí Œ≥`. Suppose that it is continuous on each ‚Äúvertical section‚Äù
`{a} √ó univ` for `a : Œ±` from a dense set. Suppose that it is Lipschitz continuous on each
‚Äúhorizontal section‚Äù `univ √ó {b}`, `b : Œ≤` with the same Lipschitz constant `K`. Then it is
continuous.

The actual statement uses (Lipschitz) continuity of `fun y ‚Ü¶ f (a, y)` and `fun x ‚Ü¶ f (x, b)`
instead of continuity of `f` on subsets of the product space. -/
theorem continuous_prod_of_dense_continuous_lipschitzWith [PseudoEMetricSpace Œ±]
    [TopologicalSpace Œ≤] [PseudoEMetricSpace Œ≥] (f : Œ± √ó Œ≤ ‚Üí Œ≥) (K : ‚Ñù‚â•0) {s : Set Œ±}
    (hs : Dense s) (ha : ‚àÄ a ‚àà s, Continuous fun y => f (a, y))
    (hb : ‚àÄ b, LipschitzWith K fun x => f (x, b)) : Continuous f := by
  simp only [‚Üê continuousOn_univ, ‚Üê univ_prod_univ, ‚Üê lipschitzOnWith_univ] at *
  exact continuousOn_prod_of_subset_closure_continuousOn_lipschitzOnWith f (subset_univ _)
    hs.closure_eq.ge K ha fun b _ => hb b

/-- Consider a function `f : Œ± √ó Œ≤ ‚Üí Œ≥`. Suppose that it is continuous on each ‚Äúvertical section‚Äù
`{a} √ó univ`, `a : Œ±`, and is Lipschitz continuous on each ‚Äúhorizontal section‚Äù
`univ √ó {b}`, `b : Œ≤` with the same Lipschitz constant `K`. Then it is continuous.

The actual statement uses (Lipschitz) continuity of `fun y ‚Ü¶ f (a, y)` and `fun x ‚Ü¶ f (x, b)`
instead of continuity of `f` on subsets of the product space. -/
theorem continuous_prod_of_continuous_lipschitzWith [PseudoEMetricSpace Œ±] [TopologicalSpace Œ≤]
    [PseudoEMetricSpace Œ≥] (f : Œ± √ó Œ≤ ‚Üí Œ≥) (K : ‚Ñù‚â•0) (ha : ‚àÄ a, Continuous fun y => f (a, y))
    (hb : ‚àÄ b, LipschitzWith K fun x => f (x, b)) : Continuous f :=
  continuous_prod_of_dense_continuous_lipschitzWith f K dense_univ (fun _ _ ‚Ü¶ ha _) hb

theorem continuousOn_prod_of_subset_closure_continuousOn_lipschitzOnWith' [TopologicalSpace Œ±]
    [PseudoEMetricSpace Œ≤] [PseudoEMetricSpace Œ≥] (f : Œ± √ó Œ≤ ‚Üí Œ≥) {s : Set Œ±} {t t' : Set Œ≤}
    (ht' : t' ‚äÜ t) (htt' : t ‚äÜ closure t') (K : ‚Ñù‚â•0)
    (ha : ‚àÄ a ‚àà s, LipschitzOnWith K (fun y => f (a, y)) t)
    (hb : ‚àÄ b ‚àà t', ContinuousOn (fun x => f (x, b)) s) : ContinuousOn f (s √óÀ¢ t) :=
  have : ContinuousOn (f ‚àò Prod.swap) (t √óÀ¢ s) :=
    continuousOn_prod_of_subset_closure_continuousOn_lipschitzOnWith _ ht' htt' K hb ha
  this.comp continuous_swap.continuousOn (mapsTo_swap_prod _ _)

theorem continuousOn_prod_of_continuousOn_lipschitzOnWith' [TopologicalSpace Œ±]
    [PseudoEMetricSpace Œ≤] [PseudoEMetricSpace Œ≥] (f : Œ± √ó Œ≤ ‚Üí Œ≥) {s : Set Œ±} {t : Set Œ≤} (K : ‚Ñù‚â•0)
    (ha : ‚àÄ a ‚àà s, LipschitzOnWith K (fun y => f (a, y)) t)
    (hb : ‚àÄ b ‚àà t, ContinuousOn (fun x => f (x, b)) s) : ContinuousOn f (s √óÀ¢ t) :=
  have : ContinuousOn (f ‚àò Prod.swap) (t √óÀ¢ s) :=
    continuousOn_prod_of_continuousOn_lipschitzOnWith _ K hb ha
  this.comp continuous_swap.continuousOn (mapsTo_swap_prod _ _)

theorem continuous_prod_of_dense_continuous_lipschitzWith' [TopologicalSpace Œ±]
    [PseudoEMetricSpace Œ≤] [PseudoEMetricSpace Œ≥] (f : Œ± √ó Œ≤ ‚Üí Œ≥) (K : ‚Ñù‚â•0) {t : Set Œ≤}
    (ht : Dense t) (ha : ‚àÄ a, LipschitzWith K fun y => f (a, y))
    (hb : ‚àÄ b ‚àà t, Continuous fun x => f (x, b)) : Continuous f :=
  have : Continuous (f ‚àò Prod.swap) :=
    continuous_prod_of_dense_continuous_lipschitzWith _ K ht hb ha
  this.comp continuous_swap

theorem continuous_prod_of_continuous_lipschitzWith' [TopologicalSpace Œ±] [PseudoEMetricSpace Œ≤]
    [PseudoEMetricSpace Œ≥] (f : Œ± √ó Œ≤ ‚Üí Œ≥) (K : ‚Ñù‚â•0) (ha : ‚àÄ a, LipschitzWith K fun y => f (a, y))
    (hb : ‚àÄ b, Continuous fun x => f (x, b)) : Continuous f :=
  have : Continuous (f ‚àò Prod.swap) :=
    continuous_prod_of_continuous_lipschitzWith _ K hb ha
  this.comp continuous_swap
