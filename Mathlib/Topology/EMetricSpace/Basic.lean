/-
Copyright (c) 2015, 2017 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Robert Y. Lewis, Johannes HÃ¶lzl, Mario Carneiro, SÃ©bastien GouÃ«zel
-/
import Mathlib.Algebra.Order.BigOperators.Group.Finset
import Mathlib.Algebra.Order.Interval.Finset.SuccPred
import Mathlib.Data.Nat.SuccPred
import Mathlib.Order.Interval.Finset.Nat
import Mathlib.Topology.EMetricSpace.Defs
import Mathlib.Topology.UniformSpace.Compact
import Mathlib.Topology.UniformSpace.LocallyUniformConvergence
import Mathlib.Topology.UniformSpace.UniformEmbedding

/-!
# Extended metric spaces

Further results about extended metric spaces.
-/

open Set Filter

universe u v w

variable {Î± : Type u} {Î² : Type v} {X : Type*}

open scoped Uniformity Topology NNReal ENNReal Pointwise

variable [PseudoEMetricSpace Î±]

/-- The triangle (polygon) inequality for sequences of points; `Finset.Ico` version. -/
theorem edist_le_Ico_sum_edist (f : â„• â†’ Î±) {m n} (h : m â‰¤ n) :
    edist (f m) (f n) â‰¤ âˆ‘ i âˆˆ Finset.Ico m n, edist (f i) (f (i + 1)) := by
  induction n, h using Nat.le_induction with
  | base => rw [Finset.Ico_self, Finset.sum_empty, edist_self]
  | succ n hle ihn =>
    calc
      edist (f m) (f (n + 1)) â‰¤ edist (f m) (f n) + edist (f n) (f (n + 1)) := edist_triangle _ _ _
      _ â‰¤ (âˆ‘ i âˆˆ Finset.Ico m n, _) + _ := add_le_add ihn le_rfl
      _ = âˆ‘ i âˆˆ Finset.Ico m (n + 1), _ := by
        rw [â† Finset.insert_Ico_right_eq_Ico_add_one hle, Finset.sum_insert, add_comm]; simp

/-- The triangle (polygon) inequality for sequences of points; `Finset.range` version. -/
theorem edist_le_range_sum_edist (f : â„• â†’ Î±) (n : â„•) :
    edist (f 0) (f n) â‰¤ âˆ‘ i âˆˆ Finset.range n, edist (f i) (f (i + 1)) :=
  Nat.Ico_zero_eq_range â–¸ edist_le_Ico_sum_edist f (Nat.zero_le n)

/-- A version of `edist_le_Ico_sum_edist` with each intermediate distance replaced
with an upper estimate. -/
theorem edist_le_Ico_sum_of_edist_le {f : â„• â†’ Î±} {m n} (hmn : m â‰¤ n) {d : â„• â†’ â„â‰¥0âˆ}
    (hd : âˆ€ {k}, m â‰¤ k â†’ k < n â†’ edist (f k) (f (k + 1)) â‰¤ d k) :
    edist (f m) (f n) â‰¤ âˆ‘ i âˆˆ Finset.Ico m n, d i :=
  le_trans (edist_le_Ico_sum_edist f hmn) <|
    Finset.sum_le_sum fun _k hk => hd (Finset.mem_Ico.1 hk).1 (Finset.mem_Ico.1 hk).2

/-- A version of `edist_le_range_sum_edist` with each intermediate distance replaced
with an upper estimate. -/
theorem edist_le_range_sum_of_edist_le {f : â„• â†’ Î±} (n : â„•) {d : â„• â†’ â„â‰¥0âˆ}
    (hd : âˆ€ {k}, k < n â†’ edist (f k) (f (k + 1)) â‰¤ d k) :
    edist (f 0) (f n) â‰¤ âˆ‘ i âˆˆ Finset.range n, d i :=
  Nat.Ico_zero_eq_range â–¸ edist_le_Ico_sum_of_edist_le (zero_le n) fun _ => hd

namespace EMetric

theorem isUniformInducing_iff [PseudoEMetricSpace Î²] {f : Î± â†’ Î²} :
    IsUniformInducing f â†” UniformContinuous f âˆ§
      âˆ€ Î´ > 0, âˆƒ Îµ > 0, âˆ€ {a b : Î±}, edist (f a) (f b) < Îµ â†’ edist a b < Î´ :=
  isUniformInducing_iff'.trans <| Iff.rfl.and <|
    ((uniformity_basis_edist.comap _).le_basis_iff uniformity_basis_edist).trans <| by
      simp only [subset_def, Prod.forall]; rfl

/-- Îµ-Î´ characterization of uniform embeddings on pseudoemetric spaces -/
nonrec theorem isUniformEmbedding_iff [PseudoEMetricSpace Î²] {f : Î± â†’ Î²} :
    IsUniformEmbedding f â†” Function.Injective f âˆ§ UniformContinuous f âˆ§
      âˆ€ Î´ > 0, âˆƒ Îµ > 0, âˆ€ {a b : Î±}, edist (f a) (f b) < Îµ â†’ edist a b < Î´ :=
  (isUniformEmbedding_iff _).trans <| and_comm.trans <| Iff.rfl.and isUniformInducing_iff

/-- If a map between pseudoemetric spaces is a uniform embedding then the edistance between `f x`
and `f y` is controlled in terms of the distance between `x` and `y`.

In fact, this lemma holds for a `IsUniformInducing` map.
TODO: generalize? -/
theorem controlled_of_isUniformEmbedding [PseudoEMetricSpace Î²] {f : Î± â†’ Î²}
    (h : IsUniformEmbedding f) :
    (âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ {a b : Î±}, edist a b < Î´ â†’ edist (f a) (f b) < Îµ) âˆ§
      âˆ€ Î´ > 0, âˆƒ Îµ > 0, âˆ€ {a b : Î±}, edist (f a) (f b) < Îµ â†’ edist a b < Î´ :=
  âŸ¨uniformContinuous_iff.1 h.uniformContinuous, (isUniformEmbedding_iff.1 h).2.2âŸ©

/-- Îµ-Î´ characterization of Cauchy sequences on pseudoemetric spaces -/
protected theorem cauchy_iff {f : Filter Î±} :
    Cauchy f â†” f â‰  âŠ¥ âˆ§ âˆ€ Îµ > 0, âˆƒ t âˆˆ f, âˆ€ x, x âˆˆ t â†’ âˆ€ y, y âˆˆ t â†’ edist x y < Îµ := by
  rw [â† neBot_iff]; exact uniformity_basis_edist.cauchy_iff

/-- A very useful criterion to show that a space is complete is to show that all sequences
which satisfy a bound of the form `edist (u n) (u m) < B N` for all `n m â‰¥ N` are
converging. This is often applied for `B N = 2^{-N}`, i.e., with a very fast convergence to
`0`, which makes it possible to use arguments of converging series, while this is impossible
to do in general for arbitrary Cauchy sequences. -/
theorem complete_of_convergent_controlled_sequences (B : â„• â†’ â„â‰¥0âˆ) (hB : âˆ€ n, 0 < B n)
    (H : âˆ€ u : â„• â†’ Î±, (âˆ€ N n m : â„•, N â‰¤ n â†’ N â‰¤ m â†’ edist (u n) (u m) < B N) â†’
      âˆƒ x, Tendsto u atTop (ğ“ x)) :
    CompleteSpace Î± :=
  UniformSpace.complete_of_convergent_controlled_sequences
    (fun n => { p : Î± Ã— Î± | edist p.1 p.2 < B n }) (fun n => edist_mem_uniformity <| hB n) H

/-- A sequentially complete pseudoemetric space is complete. -/
theorem complete_of_cauchySeq_tendsto :
    (âˆ€ u : â„• â†’ Î±, CauchySeq u â†’ âˆƒ a, Tendsto u atTop (ğ“ a)) â†’ CompleteSpace Î± :=
  UniformSpace.complete_of_cauchySeq_tendsto

/-- Expressing locally uniform convergence on a set using `edist`. -/
theorem tendstoLocallyUniformlyOn_iff {Î¹ : Type*} [TopologicalSpace Î²] {F : Î¹ â†’ Î² â†’ Î±} {f : Î² â†’ Î±}
    {p : Filter Î¹} {s : Set Î²} :
    TendstoLocallyUniformlyOn F f p s â†”
      âˆ€ Îµ > 0, âˆ€ x âˆˆ s, âˆƒ t âˆˆ ğ“[s] x, âˆ€á¶  n in p, âˆ€ y âˆˆ t, edist (f y) (F n y) < Îµ := by
  refine âŸ¨fun H Îµ hÎµ => H _ (edist_mem_uniformity hÎµ), fun H u hu x hx => ?_âŸ©
  rcases mem_uniformity_edist.1 hu with âŸ¨Îµ, Îµpos, hÎµâŸ©
  rcases H Îµ Îµpos x hx with âŸ¨t, ht, HtâŸ©
  exact âŸ¨t, ht, Ht.mono fun n hs x hx => hÎµ (hs x hx)âŸ©

/-- Expressing uniform convergence on a set using `edist`. -/
theorem tendstoUniformlyOn_iff {Î¹ : Type*} {F : Î¹ â†’ Î² â†’ Î±} {f : Î² â†’ Î±} {p : Filter Î¹} {s : Set Î²} :
    TendstoUniformlyOn F f p s â†” âˆ€ Îµ > 0, âˆ€á¶  n in p, âˆ€ x âˆˆ s, edist (f x) (F n x) < Îµ := by
  refine âŸ¨fun H Îµ hÎµ => H _ (edist_mem_uniformity hÎµ), fun H u hu => ?_âŸ©
  rcases mem_uniformity_edist.1 hu with âŸ¨Îµ, Îµpos, hÎµâŸ©
  exact (H Îµ Îµpos).mono fun n hs x hx => hÎµ (hs x hx)

/-- Expressing locally uniform convergence using `edist`. -/
theorem tendstoLocallyUniformly_iff {Î¹ : Type*} [TopologicalSpace Î²] {F : Î¹ â†’ Î² â†’ Î±} {f : Î² â†’ Î±}
    {p : Filter Î¹} :
    TendstoLocallyUniformly F f p â†”
      âˆ€ Îµ > 0, âˆ€ x : Î², âˆƒ t âˆˆ ğ“ x, âˆ€á¶  n in p, âˆ€ y âˆˆ t, edist (f y) (F n y) < Îµ := by
  simp only [â† tendstoLocallyUniformlyOn_univ, tendstoLocallyUniformlyOn_iff, mem_univ,
    forall_const, nhdsWithin_univ]

/-- Expressing uniform convergence using `edist`. -/
theorem tendstoUniformly_iff {Î¹ : Type*} {F : Î¹ â†’ Î² â†’ Î±} {f : Î² â†’ Î±} {p : Filter Î¹} :
    TendstoUniformly F f p â†” âˆ€ Îµ > 0, âˆ€á¶  n in p, âˆ€ x, edist (f x) (F n x) < Îµ := by
  simp only [â† tendstoUniformlyOn_univ, tendstoUniformlyOn_iff, mem_univ, forall_const]

end EMetric

open EMetric

namespace EMetric

variable {x y z : Î±} {Îµ Îµâ‚ Îµâ‚‚ : â„â‰¥0âˆ} {s t : Set Î±}

theorem inseparable_iff : Inseparable x y â†” edist x y = 0 := by
  simp [inseparable_iff_mem_closure, mem_closure_iff, edist_comm, forall_gt_iff_le]

alias âŸ¨_root_.Inseparable.edist_eq_zero, _âŸ© := EMetric.inseparable_iff

-- see Note [nolint_ge]
/-- In a pseudoemetric space, Cauchy sequences are characterized by the fact that, eventually,
the pseudoedistance between its elements is arbitrarily small -/
theorem cauchySeq_iff [Nonempty Î²] [SemilatticeSup Î²] {u : Î² â†’ Î±} :
    CauchySeq u â†” âˆ€ Îµ > 0, âˆƒ N, âˆ€ m, N â‰¤ m â†’ âˆ€ n, N â‰¤ n â†’ edist (u m) (u n) < Îµ :=
  uniformity_basis_edist.cauchySeq_iff

/-- A variation around the emetric characterization of Cauchy sequences -/
theorem cauchySeq_iff' [Nonempty Î²] [SemilatticeSup Î²] {u : Î² â†’ Î±} :
    CauchySeq u â†” âˆ€ Îµ > (0 : â„â‰¥0âˆ), âˆƒ N, âˆ€ n â‰¥ N, edist (u n) (u N) < Îµ :=
  uniformity_basis_edist.cauchySeq_iff'

/-- A variation of the emetric characterization of Cauchy sequences that deals with
`â„â‰¥0` upper bounds. -/
theorem cauchySeq_iff_NNReal [Nonempty Î²] [SemilatticeSup Î²] {u : Î² â†’ Î±} :
    CauchySeq u â†” âˆ€ Îµ : â„â‰¥0, 0 < Îµ â†’ âˆƒ N, âˆ€ n, N â‰¤ n â†’ edist (u n) (u N) < Îµ :=
  uniformity_basis_edist_nnreal.cauchySeq_iff'

theorem totallyBounded_iff {s : Set Î±} :
    TotallyBounded s â†” âˆ€ Îµ > 0, âˆƒ t : Set Î±, t.Finite âˆ§ s âŠ† â‹ƒ y âˆˆ t, ball y Îµ :=
  âŸ¨fun H _Îµ Îµ0 => H _ (edist_mem_uniformity Îµ0), fun H _r ru =>
    let âŸ¨Îµ, Îµ0, hÎµâŸ© := mem_uniformity_edist.1 ru
    let âŸ¨t, ft, hâŸ© := H Îµ Îµ0
    âŸ¨t, ft, h.trans <| iUnionâ‚‚_mono fun _ _ _ => hÎµâŸ©âŸ©

theorem totallyBounded_iff' {s : Set Î±} :
    TotallyBounded s â†” âˆ€ Îµ > 0, âˆƒ t, t âŠ† s âˆ§ Set.Finite t âˆ§ s âŠ† â‹ƒ y âˆˆ t, ball y Îµ :=
  âŸ¨fun H _Îµ Îµ0 => (totallyBounded_iff_subset.1 H) _ (edist_mem_uniformity Îµ0), fun H _r ru =>
    let âŸ¨Îµ, Îµ0, hÎµâŸ© := mem_uniformity_edist.1 ru
    let âŸ¨t, _, ft, hâŸ© := H Îµ Îµ0
    âŸ¨t, ft, h.trans <| iUnionâ‚‚_mono fun _ _ _ => hÎµâŸ©âŸ©

section Compact

-- TODO: generalize to metrizable spaces
/-- A compact set in a pseudo emetric space is separable, i.e., it is a subset of the closure of a
countable set. -/
theorem subset_countable_closure_of_compact {s : Set Î±} (hs : IsCompact s) :
    âˆƒ t, t âŠ† s âˆ§ t.Countable âˆ§ s âŠ† closure t := by
  refine subset_countable_closure_of_almost_dense_set s fun Îµ hÎµ => ?_
  rcases totallyBounded_iff'.1 hs.totallyBounded Îµ hÎµ with âŸ¨t, -, htf, hstâŸ©
  exact âŸ¨t, htf.countable, hst.trans <| iUnionâ‚‚_mono fun _ _ => ball_subset_closedBallâŸ©

end Compact

section SecondCountable

open TopologicalSpace

variable (Î±) in
/-- A sigma compact pseudo emetric space has second countable topology. -/
instance (priority := 90) secondCountable_of_sigmaCompact [SigmaCompactSpace Î±] :
    SecondCountableTopology Î± := by
  suffices SeparableSpace Î± by exact UniformSpace.secondCountable_of_separable Î±
  choose T _ hTc hsubT using fun n =>
    subset_countable_closure_of_compact (isCompact_compactCovering Î± n)
  refine âŸ¨âŸ¨â‹ƒ n, T n, countable_iUnion hTc, fun x => ?_âŸ©âŸ©
  rcases iUnion_eq_univ_iff.1 (iUnion_compactCovering Î±) x with âŸ¨n, hnâŸ©
  exact closure_mono (subset_iUnion _ n) (hsubT _ hn)

theorem secondCountable_of_almost_dense_set
    (hs : âˆ€ Îµ > 0, âˆƒ t : Set Î±, t.Countable âˆ§ â‹ƒ x âˆˆ t, closedBall x Îµ = univ) :
    SecondCountableTopology Î± := by
  suffices SeparableSpace Î± from UniformSpace.secondCountable_of_separable Î±
  have : âˆ€ Îµ > 0, âˆƒ t : Set Î±, Set.Countable t âˆ§ univ âŠ† â‹ƒ x âˆˆ t, closedBall x Îµ := by
    simpa only [univ_subset_iff] using hs
  rcases subset_countable_closure_of_almost_dense_set (univ : Set Î±) this with âŸ¨t, -, htc, htâŸ©
  exact âŸ¨âŸ¨t, htc, fun x => ht (mem_univ x)âŸ©âŸ©

end SecondCountable

end EMetric

variable {Î³ : Type w} [EMetricSpace Î³]

-- see Note [lower instance priority]
/-- An emetric space is separated -/
instance (priority := 100) EMetricSpace.instT0Space : T0Space Î³ where
  t0 _ _ h := eq_of_edist_eq_zero <| inseparable_iff.1 h

/-- A map between emetric spaces is a uniform embedding if and only if the edistance between `f x`
and `f y` is controlled in terms of the distance between `x` and `y` and conversely. -/
theorem EMetric.isUniformEmbedding_iff' [PseudoEMetricSpace Î²] {f : Î³ â†’ Î²} :
    IsUniformEmbedding f â†”
      (âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ {a b : Î³}, edist a b < Î´ â†’ edist (f a) (f b) < Îµ) âˆ§
        âˆ€ Î´ > 0, âˆƒ Îµ > 0, âˆ€ {a b : Î³}, edist (f a) (f b) < Îµ â†’ edist a b < Î´ := by
  rw [isUniformEmbedding_iff_isUniformInducing, isUniformInducing_iff, uniformContinuous_iff]

/-- If a `PseudoEMetricSpace` is a Tâ‚€ space, then it is an `EMetricSpace`. -/
-- TODO: make it an instance?
abbrev EMetricSpace.ofT0PseudoEMetricSpace (Î± : Type*) [PseudoEMetricSpace Î±] [T0Space Î±] :
    EMetricSpace Î± :=
  { â€¹PseudoEMetricSpace Î±â€º with
    eq_of_edist_eq_zero := fun h => (EMetric.inseparable_iff.2 h).eq }

/-- The product of two emetric spaces, with the max distance, is an extended
metric spaces. We make sure that the uniform structure thus constructed is the one
corresponding to the product of uniform spaces, to avoid diamond problems. -/
instance Prod.emetricSpaceMax [EMetricSpace Î²] : EMetricSpace (Î³ Ã— Î²) :=
  .ofT0PseudoEMetricSpace _

namespace EMetric

/-- A compact set in an emetric space is separable, i.e., it is the closure of a countable set. -/
theorem countable_closure_of_compact {s : Set Î³} (hs : IsCompact s) :
    âˆƒ t, t âŠ† s âˆ§ t.Countable âˆ§ s = closure t := by
  rcases subset_countable_closure_of_compact hs with âŸ¨t, hts, htc, hsubâŸ©
  exact âŸ¨t, hts, htc, hsub.antisymm (closure_minimal hts hs.isClosed)âŸ©

end EMetric

/-!
### Separation quotient
-/

instance [PseudoEMetricSpace X] : EDist (SeparationQuotient X) where
  edist := SeparationQuotient.liftâ‚‚ edist fun _ _ _ _ hx hy =>
    edist_congr (EMetric.inseparable_iff.1 hx) (EMetric.inseparable_iff.1 hy)

@[simp] theorem SeparationQuotient.edist_mk [PseudoEMetricSpace X] (x y : X) :
    edist (mk x) (mk y) = edist x y :=
  rfl

open SeparationQuotient in
instance [PseudoEMetricSpace X] : EMetricSpace (SeparationQuotient X) :=
  @EMetricSpace.ofT0PseudoEMetricSpace (SeparationQuotient X)
    { edist_self := surjective_mk.forall.2 edist_self,
      edist_comm := surjective_mk.forallâ‚‚.2 edist_comm,
      edist_triangle := surjective_mk.forallâ‚ƒ.2 edist_triangle,
      toUniformSpace := inferInstance,
      uniformity_edist := comap_injective (surjective_mk.prodMap surjective_mk) <| by
        simp [comap_mk_uniformity, PseudoEMetricSpace.uniformity_edist] } _

namespace TopologicalSpace

section Compact

open Topology

/-- If a set `s` is separable in a (pseudo extended) metric space, then it admits a countable dense
subset. This is not obvious, as the countable set whose closure covers `s` given by the definition
of separability does not need in general to be contained in `s`. -/
theorem IsSeparable.exists_countable_dense_subset
    {s : Set Î±} (hs : IsSeparable s) : âˆƒ t, t âŠ† s âˆ§ t.Countable âˆ§ s âŠ† closure t := by
  have : âˆ€ Îµ > 0, âˆƒ t : Set Î±, t.Countable âˆ§ s âŠ† â‹ƒ x âˆˆ t, closedBall x Îµ := fun Îµ Îµ0 => by
    rcases hs with âŸ¨t, htc, hstâŸ©
    refine âŸ¨t, htc, hst.trans fun x hx => ?_âŸ©
    rcases mem_closure_iff.1 hx Îµ Îµ0 with âŸ¨y, hyt, hxyâŸ©
    exact mem_iUnionâ‚‚.2 âŸ¨y, hyt, mem_closedBall.2 hxy.leâŸ©
  exact subset_countable_closure_of_almost_dense_set _ this

/-- If a set `s` is separable, then the corresponding subtype is separable in a (pseudo extended)
metric space.  This is not obvious, as the countable set whose closure covers `s` does not need in
general to be contained in `s`. -/
theorem IsSeparable.separableSpace {s : Set Î±} (hs : IsSeparable s) :
    SeparableSpace s := by
  rcases hs.exists_countable_dense_subset with âŸ¨t, hts, htc, hstâŸ©
  lift t to Set s using hts
  refine âŸ¨âŸ¨t, countable_of_injective_of_countable_image Subtype.coe_injective.injOn htc, ?_âŸ©âŸ©
  rwa [IsInducing.subtypeVal.dense_iff, Subtype.forall]

end Compact

end TopologicalSpace

section LebesgueNumberLemma

variable {s : Set Î±}

theorem lebesgue_number_lemma_of_emetric {Î¹ : Sort*} {c : Î¹ â†’ Set Î±} (hs : IsCompact s)
    (hcâ‚ : âˆ€ i, IsOpen (c i)) (hcâ‚‚ : s âŠ† â‹ƒ i, c i) : âˆƒ Î´ > 0, âˆ€ x âˆˆ s, âˆƒ i, ball x Î´ âŠ† c i := by
  simpa only [ball, UniformSpace.ball, preimage_setOf_eq, edist_comm]
    using uniformity_basis_edist.lebesgue_number_lemma hs hcâ‚ hcâ‚‚

theorem lebesgue_number_lemma_of_emetric_nhds' {c : (x : Î±) â†’ x âˆˆ s â†’ Set Î±} (hs : IsCompact s)
    (hc : âˆ€ x hx, c x hx âˆˆ ğ“ x) : âˆƒ Î´ > 0, âˆ€ x âˆˆ s, âˆƒ y : s, ball x Î´ âŠ† c y y.2 := by
  simpa only [ball, UniformSpace.ball, preimage_setOf_eq, edist_comm]
    using uniformity_basis_edist.lebesgue_number_lemma_nhds' hs hc

theorem lebesgue_number_lemma_of_emetric_nhds {c : Î± â†’ Set Î±} (hs : IsCompact s)
    (hc : âˆ€ x âˆˆ s, c x âˆˆ ğ“ x) : âˆƒ Î´ > 0, âˆ€ x âˆˆ s, âˆƒ y, ball x Î´ âŠ† c y := by
  simpa only [ball, UniformSpace.ball, preimage_setOf_eq, edist_comm]
    using uniformity_basis_edist.lebesgue_number_lemma_nhds hs hc

theorem lebesgue_number_lemma_of_emetric_nhdsWithin' {c : (x : Î±) â†’ x âˆˆ s â†’ Set Î±}
    (hs : IsCompact s) (hc : âˆ€ x hx, c x hx âˆˆ ğ“[s] x) :
    âˆƒ Î´ > 0, âˆ€ x âˆˆ s, âˆƒ y : s, ball x Î´ âˆ© s âŠ† c y y.2 := by
  simpa only [ball, UniformSpace.ball, preimage_setOf_eq, edist_comm]
    using uniformity_basis_edist.lebesgue_number_lemma_nhdsWithin' hs hc

theorem lebesgue_number_lemma_of_emetric_nhdsWithin {c : Î± â†’ Set Î±} (hs : IsCompact s)
    (hc : âˆ€ x âˆˆ s, c x âˆˆ ğ“[s] x) : âˆƒ Î´ > 0, âˆ€ x âˆˆ s, âˆƒ y, ball x Î´ âˆ© s âŠ† c y := by
  simpa only [ball, UniformSpace.ball, preimage_setOf_eq, edist_comm]
    using uniformity_basis_edist.lebesgue_number_lemma_nhdsWithin hs hc

theorem lebesgue_number_lemma_of_emetric_sUnion {c : Set (Set Î±)} (hs : IsCompact s)
    (hcâ‚ : âˆ€ t âˆˆ c, IsOpen t) (hcâ‚‚ : s âŠ† â‹ƒâ‚€ c) : âˆƒ Î´ > 0, âˆ€ x âˆˆ s, âˆƒ t âˆˆ c, ball x Î´ âŠ† t := by
  rw [sUnion_eq_iUnion] at hcâ‚‚; simpa using lebesgue_number_lemma_of_emetric hs (by simpa) hcâ‚‚

end LebesgueNumberLemma
