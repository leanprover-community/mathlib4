/-
Copyright (c) 2015, 2017 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Robert Y. Lewis, Johannes HÃ¶lzl, Mario Carneiro, SÃ©bastien GouÃ«zel
-/
import Mathlib.Data.ENNReal.Inv
import Mathlib.Topology.UniformSpace.Basic
import Mathlib.Topology.UniformSpace.OfFun

/-!
# Extended metric spaces

This file is devoted to the definition and study of `EMetricSpace`s, i.e., metric
spaces in which the distance is allowed to take the value âˆ. This extended distance is
called `edist`, and takes values in `â„â‰¥0âˆ`.

Many definitions and theorems expected on emetric spaces are already introduced on uniform spaces
and topological spaces. For example: open and closed sets, compactness, completeness, continuity and
uniform continuity.

The class `EMetricSpace` therefore extends `UniformSpace` (and `TopologicalSpace`).

Since a lot of elementary properties don't require `eq_of_edist_eq_zero` we start setting up the
theory of `PseudoEMetricSpace`, where we don't require `edist x y = 0 â†’ x = y` and we specialize
to `EMetricSpace` at the end.
-/


assert_not_exists Nat.instLocallyFiniteOrder IsUniformEmbedding TendstoUniformlyOnFilter

open Filter Set Topology

universe u v w

variable {Î± : Type u} {Î² : Type v} {X : Type*}

/-- Characterizing uniformities associated to a (generalized) distance function `D`
in terms of the elements of the uniformity. -/
theorem uniformity_dist_of_mem_uniformity [LT Î²] {U : Filter (Î± Ã— Î±)} (z : Î²)
    (D : Î± â†’ Î± â†’ Î²) (H : âˆ€ s, s âˆˆ U â†” âˆƒ Îµ > z, âˆ€ {a b : Î±}, D a b < Îµ â†’ (a, b) âˆˆ s) :
    U = â¨… Îµ > z, ğ“Ÿ { p : Î± Ã— Î± | D p.1 p.2 < Îµ } :=
  HasBasis.eq_biInf âŸ¨fun s => by simp only [H, subset_def, Prod.forall, mem_setOf]âŸ©

open scoped Uniformity Topology Filter NNReal ENNReal Pointwise

/-- `EDist Î±` means that `Î±` is equipped with an extended distance. -/
@[ext]
class EDist (Î± : Type*) where
  /-- Extended distance between two points -/
  edist : Î± â†’ Î± â†’ â„â‰¥0âˆ

export EDist (edist)

/-- Creating a uniform space from an extended distance. -/
@[reducible] def uniformSpaceOfEDist (edist : Î± â†’ Î± â†’ â„â‰¥0âˆ) (edist_self : âˆ€ x : Î±, edist x x = 0)
    (edist_comm : âˆ€ x y : Î±, edist x y = edist y x)
    (edist_triangle : âˆ€ x y z : Î±, edist x z â‰¤ edist x y + edist y z) : UniformSpace Î± :=
  .ofFun edist edist_self edist_comm edist_triangle fun Îµ Îµ0 =>
    âŸ¨Îµ / 2, ENNReal.half_pos Îµ0.ne', fun _ hâ‚ _ hâ‚‚ =>
      (ENNReal.add_lt_add hâ‚ hâ‚‚).trans_eq (ENNReal.add_halves _)âŸ©

/-- Creating a uniform space from an extended distance. We assume that
there is a preexisting topology, for which the neighborhoods can be expressed using the distance,
and we make sure that the uniform space structure we construct has a topology which is defeq
to the original one. -/
@[reducible] noncomputable def uniformSpaceOfEDistOfHasBasis [TopologicalSpace Î±]
    (edist : Î± â†’ Î± â†’ â„â‰¥0âˆ)
    (edist_self : âˆ€ x : Î±, edist x x = 0)
    (edist_comm : âˆ€ x y : Î±, edist x y = edist y x)
    (edist_triangle : âˆ€ x y z : Î±, edist x z â‰¤ edist x y + edist y z)
    (basis : âˆ€ x, (ğ“ x).HasBasis (fun c â†¦ 0 < c) (fun c â†¦ {y | edist x y < c})) :
    UniformSpace Î± :=
  .ofFunOfHasBasis edist edist_self edist_comm edist_triangle (fun Îµ Îµ0 =>
    âŸ¨Îµ / 2, ENNReal.half_pos Îµ0.ne', fun _ hâ‚ _ hâ‚‚ =>
      (ENNReal.add_lt_add hâ‚ hâ‚‚).trans_eq (ENNReal.add_halves _)âŸ©) basis

/-- A pseudo extended metric space is a type endowed with a `â„â‰¥0âˆ`-valued distance `edist`
satisfying reflexivity `edist x x = 0`, commutativity `edist x y = edist y x`, and the triangle
inequality `edist x z â‰¤ edist x y + edist y z`.

Note that we do not require `edist x y = 0 â†’ x = y`. See extended metric spaces (`EMetricSpace`) for
the similar class with that stronger assumption.

Any pseudo extended metric space is a topological space and a uniform space (see `TopologicalSpace`,
`UniformSpace`), where the topology and uniformity come from the metric.
Note that a T1 pseudo extended metric space is just an extended metric space.

We make the uniformity/topology part of the data instead of deriving it from the metric. This eg
ensures that we do not get a diamond when doing
`[PseudoEMetricSpace Î±] [PseudoEMetricSpace Î²] : TopologicalSpace (Î± Ã— Î²)`:
The product metric and product topology agree, but not definitionally so.
See Note [forgetful inheritance]. -/
class PseudoEMetricSpace (Î± : Type u) : Type u extends EDist Î±  where
  edist_self : âˆ€ x : Î±, edist x x = 0
  edist_comm : âˆ€ x y : Î±, edist x y = edist y x
  edist_triangle : âˆ€ x y z : Î±, edist x z â‰¤ edist x y + edist y z
  toUniformSpace : UniformSpace Î± := uniformSpaceOfEDist edist edist_self edist_comm edist_triangle
  uniformity_edist : ğ“¤ Î± = â¨… Îµ > 0, ğ“Ÿ { p : Î± Ã— Î± | edist p.1 p.2 < Îµ } := by rfl

attribute [instance] PseudoEMetricSpace.toUniformSpace

/- Pseudoemetric spaces are less common than metric spaces. Therefore, we work in a dedicated
namespace, while notions associated to metric spaces are mostly in the root namespace. -/

/-- Two pseudo emetric space structures with the same edistance function coincide. -/
@[ext]
protected theorem PseudoEMetricSpace.ext {Î± : Type*} {m m' : PseudoEMetricSpace Î±}
    (h : m.toEDist = m'.toEDist) : m = m' := by
  obtain âŸ¨_, _, _, U, hUâŸ© := m; rename EDist Î± => ed
  obtain âŸ¨_, _, _, U', hU'âŸ© := m'; rename EDist Î± => ed'
  congr 1
  exact UniformSpace.ext (((show ed = ed' from h) â–¸ hU).trans hU'.symm)

variable [PseudoEMetricSpace Î±]

export PseudoEMetricSpace (edist_self edist_comm edist_triangle)

attribute [simp] edist_self

/-- Triangle inequality for the extended distance -/
theorem edist_triangle_left (x y z : Î±) : edist x y â‰¤ edist z x + edist z y := by
  rw [edist_comm z]; apply edist_triangle

theorem edist_triangle_right (x y z : Î±) : edist x y â‰¤ edist x z + edist y z := by
  rw [edist_comm y]; apply edist_triangle

theorem edist_congr_right {x y z : Î±} (h : edist x y = 0) : edist x z = edist y z := by
  apply le_antisymm
  Â· rw [â† zero_add (edist y z), â† h]
    apply edist_triangle
  Â· rw [edist_comm] at h
    rw [â† zero_add (edist x z), â† h]
    apply edist_triangle

theorem edist_congr_left {x y z : Î±} (h : edist x y = 0) : edist z x = edist z y := by
  rw [edist_comm z x, edist_comm z y]
  apply edist_congr_right h

-- new theorem
theorem edist_congr {w x y z : Î±} (hl : edist w x = 0) (hr : edist y z = 0) :
    edist w y = edist x z :=
  (edist_congr_right hl).trans (edist_congr_left hr)

theorem edist_triangle4 (x y z t : Î±) : edist x t â‰¤ edist x y + edist y z + edist z t :=
  calc
    edist x t â‰¤ edist x z + edist z t := edist_triangle x z t
    _ â‰¤ edist x y + edist y z + edist z t := add_le_add_right (edist_triangle x y z) _

/-- Reformulation of the uniform structure in terms of the extended distance -/
theorem uniformity_pseudoedist : ğ“¤ Î± = â¨… Îµ > 0, ğ“Ÿ { p : Î± Ã— Î± | edist p.1 p.2 < Îµ } :=
  PseudoEMetricSpace.uniformity_edist

theorem uniformSpace_edist :
    â€¹PseudoEMetricSpace Î±â€º.toUniformSpace =
      uniformSpaceOfEDist edist edist_self edist_comm edist_triangle :=
  UniformSpace.ext uniformity_pseudoedist

theorem uniformity_basis_edist :
    (ğ“¤ Î±).HasBasis (fun Îµ : â„â‰¥0âˆ => 0 < Îµ) fun Îµ => { p : Î± Ã— Î± | edist p.1 p.2 < Îµ } :=
  (@uniformSpace_edist Î± _).symm â–¸ UniformSpace.hasBasis_ofFun âŸ¨1, one_posâŸ© _ _ _ _ _

/-- Characterization of the elements of the uniformity in terms of the extended distance -/
theorem mem_uniformity_edist {s : Set (Î± Ã— Î±)} :
    s âˆˆ ğ“¤ Î± â†” âˆƒ Îµ > 0, âˆ€ {a b : Î±}, edist a b < Îµ â†’ (a, b) âˆˆ s :=
  uniformity_basis_edist.mem_uniformity_iff

/-- Given `f : Î² â†’ â„â‰¥0âˆ`, if `f` sends `{i | p i}` to a set of positive numbers
accumulating to zero, then `f i`-neighborhoods of the diagonal form a basis of `ğ“¤ Î±`.

For specific bases see `uniformity_basis_edist`, `uniformity_basis_edist'`,
`uniformity_basis_edist_nnreal`, and `uniformity_basis_edist_inv_nat`. -/
protected theorem EMetric.mk_uniformity_basis {Î² : Type*} {p : Î² â†’ Prop} {f : Î² â†’ â„â‰¥0âˆ}
    (hfâ‚€ : âˆ€ x, p x â†’ 0 < f x) (hf : âˆ€ Îµ, 0 < Îµ â†’ âˆƒ x, p x âˆ§ f x â‰¤ Îµ) :
    (ğ“¤ Î±).HasBasis p fun x => { p : Î± Ã— Î± | edist p.1 p.2 < f x } := by
  refine âŸ¨fun s => uniformity_basis_edist.mem_iff.trans ?_âŸ©
  constructor
  Â· rintro âŸ¨Îµ, Îµâ‚€, hÎµâŸ©
    rcases hf Îµ Îµâ‚€ with âŸ¨i, hi, HâŸ©
    exact âŸ¨i, hi, fun x hx => hÎµ <| lt_of_lt_of_le hx.out HâŸ©
  Â· exact fun âŸ¨i, hi, HâŸ© => âŸ¨f i, hfâ‚€ i hi, HâŸ©

/-- Given `f : Î² â†’ â„â‰¥0âˆ`, if `f` sends `{i | p i}` to a set of positive numbers
accumulating to zero, then closed `f i`-neighborhoods of the diagonal form a basis of `ğ“¤ Î±`.

For specific bases see `uniformity_basis_edist_le` and `uniformity_basis_edist_le'`. -/
protected theorem EMetric.mk_uniformity_basis_le {Î² : Type*} {p : Î² â†’ Prop} {f : Î² â†’ â„â‰¥0âˆ}
    (hfâ‚€ : âˆ€ x, p x â†’ 0 < f x) (hf : âˆ€ Îµ, 0 < Îµ â†’ âˆƒ x, p x âˆ§ f x â‰¤ Îµ) :
    (ğ“¤ Î±).HasBasis p fun x => { p : Î± Ã— Î± | edist p.1 p.2 â‰¤ f x } := by
  refine âŸ¨fun s => uniformity_basis_edist.mem_iff.trans ?_âŸ©
  constructor
  Â· rintro âŸ¨Îµ, Îµâ‚€, hÎµâŸ©
    rcases exists_between Îµâ‚€ with âŸ¨Îµ', hÎµ'âŸ©
    rcases hf Îµ' hÎµ'.1 with âŸ¨i, hi, HâŸ©
    exact âŸ¨i, hi, fun x hx => hÎµ <| lt_of_le_of_lt (le_trans hx.out H) hÎµ'.2âŸ©
  Â· exact fun âŸ¨i, hi, HâŸ© => âŸ¨f i, hfâ‚€ i hi, fun x hx => H (le_of_lt hx.out)âŸ©

theorem uniformity_basis_edist_le :
    (ğ“¤ Î±).HasBasis (fun Îµ : â„â‰¥0âˆ => 0 < Îµ) fun Îµ => { p : Î± Ã— Î± | edist p.1 p.2 â‰¤ Îµ } :=
  EMetric.mk_uniformity_basis_le (fun _ => id) fun Îµ Îµâ‚€ => âŸ¨Îµ, Îµâ‚€, le_refl ÎµâŸ©

theorem uniformity_basis_edist' (Îµ' : â„â‰¥0âˆ) (hÎµ' : 0 < Îµ') :
    (ğ“¤ Î±).HasBasis (fun Îµ : â„â‰¥0âˆ => Îµ âˆˆ Ioo 0 Îµ') fun Îµ => { p : Î± Ã— Î± | edist p.1 p.2 < Îµ } :=
  EMetric.mk_uniformity_basis (fun _ => And.left) fun Îµ Îµâ‚€ =>
    let âŸ¨Î´, hÎ´âŸ© := exists_between hÎµ'
    âŸ¨min Îµ Î´, âŸ¨lt_min Îµâ‚€ hÎ´.1, lt_of_le_of_lt (min_le_right _ _) hÎ´.2âŸ©, min_le_left _ _âŸ©

theorem uniformity_basis_edist_le' (Îµ' : â„â‰¥0âˆ) (hÎµ' : 0 < Îµ') :
    (ğ“¤ Î±).HasBasis (fun Îµ : â„â‰¥0âˆ => Îµ âˆˆ Ioo 0 Îµ') fun Îµ => { p : Î± Ã— Î± | edist p.1 p.2 â‰¤ Îµ } :=
  EMetric.mk_uniformity_basis_le (fun _ => And.left) fun Îµ Îµâ‚€ =>
    let âŸ¨Î´, hÎ´âŸ© := exists_between hÎµ'
    âŸ¨min Îµ Î´, âŸ¨lt_min Îµâ‚€ hÎ´.1, lt_of_le_of_lt (min_le_right _ _) hÎ´.2âŸ©, min_le_left _ _âŸ©

theorem uniformity_basis_edist_nnreal :
    (ğ“¤ Î±).HasBasis (fun Îµ : â„â‰¥0 => 0 < Îµ) fun Îµ => { p : Î± Ã— Î± | edist p.1 p.2 < Îµ } :=
  EMetric.mk_uniformity_basis (fun _ => ENNReal.coe_pos.2) fun _Îµ Îµâ‚€ =>
    let âŸ¨Î´, hÎ´âŸ© := ENNReal.lt_iff_exists_nnreal_btwn.1 Îµâ‚€
    âŸ¨Î´, ENNReal.coe_pos.1 hÎ´.1, le_of_lt hÎ´.2âŸ©

theorem uniformity_basis_edist_nnreal_le :
    (ğ“¤ Î±).HasBasis (fun Îµ : â„â‰¥0 => 0 < Îµ) fun Îµ => { p : Î± Ã— Î± | edist p.1 p.2 â‰¤ Îµ } :=
  EMetric.mk_uniformity_basis_le (fun _ => ENNReal.coe_pos.2) fun _Îµ Îµâ‚€ =>
    let âŸ¨Î´, hÎ´âŸ© := ENNReal.lt_iff_exists_nnreal_btwn.1 Îµâ‚€
    âŸ¨Î´, ENNReal.coe_pos.1 hÎ´.1, le_of_lt hÎ´.2âŸ©

theorem uniformity_basis_edist_inv_nat :
    (ğ“¤ Î±).HasBasis (fun _ => True) fun n : â„• => { p : Î± Ã— Î± | edist p.1 p.2 < (â†‘n)â»Â¹ } :=
  EMetric.mk_uniformity_basis (fun n _ â†¦ ENNReal.inv_pos.2 <| ENNReal.natCast_ne_top n) fun _Îµ Îµâ‚€ â†¦
    let âŸ¨n, hnâŸ© := ENNReal.exists_inv_nat_lt (ne_of_gt Îµâ‚€)
    âŸ¨n, trivial, le_of_lt hnâŸ©

theorem uniformity_basis_edist_inv_two_pow :
    (ğ“¤ Î±).HasBasis (fun _ => True) fun n : â„• => { p : Î± Ã— Î± | edist p.1 p.2 < 2â»Â¹ ^ n } :=
  EMetric.mk_uniformity_basis (fun _ _ â†¦ ENNReal.pow_pos (ENNReal.inv_pos.2 ENNReal.ofNat_ne_top) _)
    fun _Îµ Îµâ‚€ â†¦
    let âŸ¨n, hnâŸ© := ENNReal.exists_inv_two_pow_lt (ne_of_gt Îµâ‚€)
    âŸ¨n, trivial, le_of_lt hnâŸ©

/-- Fixed size neighborhoods of the diagonal belong to the uniform structure -/
theorem edist_mem_uniformity {Îµ : â„â‰¥0âˆ} (Îµ0 : 0 < Îµ) : { p : Î± Ã— Î± | edist p.1 p.2 < Îµ } âˆˆ ğ“¤ Î± :=
  mem_uniformity_edist.2 âŸ¨Îµ, Îµ0, idâŸ©

namespace EMetric

instance (priority := 900) instIsCountablyGeneratedUniformity : IsCountablyGenerated (ğ“¤ Î±) :=
  isCountablyGenerated_of_seq âŸ¨_, uniformity_basis_edist_inv_nat.eq_iInfâŸ©

/-- Îµ-Î´ characterization of uniform continuity on a set for pseudoemetric spaces -/
theorem uniformContinuousOn_iff [PseudoEMetricSpace Î²] {f : Î± â†’ Î²} {s : Set Î±} :
    UniformContinuousOn f s â†”
      âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ {a}, a âˆˆ s â†’ âˆ€ {b}, b âˆˆ s â†’ edist a b < Î´ â†’ edist (f a) (f b) < Îµ :=
  uniformity_basis_edist.uniformContinuousOn_iff uniformity_basis_edist

/-- Îµ-Î´ characterization of uniform continuity on pseudoemetric spaces -/
theorem uniformContinuous_iff [PseudoEMetricSpace Î²] {f : Î± â†’ Î²} :
    UniformContinuous f â†” âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ {a b : Î±}, edist a b < Î´ â†’ edist (f a) (f b) < Îµ :=
  uniformity_basis_edist.uniformContinuous_iff uniformity_basis_edist

end EMetric

open EMetric

/-- Auxiliary function to replace the uniformity on a pseudoemetric space with
a uniformity which is equal to the original one, but maybe not defeq.
This is useful if one wants to construct a pseudoemetric space with a
specified uniformity. See Note [forgetful inheritance] explaining why having definitionally
the right uniformity is often important.
See note [reducible non-instances].
-/
abbrev PseudoEMetricSpace.replaceUniformity {Î±} [U : UniformSpace Î±] (m : PseudoEMetricSpace Î±)
    (H : ğ“¤[U] = ğ“¤[PseudoEMetricSpace.toUniformSpace]) : PseudoEMetricSpace Î± where
  edist := @edist _ m.toEDist
  edist_self := edist_self
  edist_comm := edist_comm
  edist_triangle := edist_triangle
  toUniformSpace := U
  uniformity_edist := H.trans (@PseudoEMetricSpace.uniformity_edist Î± _)

/-- The extended pseudometric induced by a function taking values in a pseudoemetric space.
See note [reducible non-instances]. -/
abbrev PseudoEMetricSpace.induced {Î± Î²} (f : Î± â†’ Î²) (m : PseudoEMetricSpace Î²) :
    PseudoEMetricSpace Î± where
  edist x y := edist (f x) (f y)
  edist_self _ := edist_self _
  edist_comm _ _ := edist_comm _ _
  edist_triangle _ _ _ := edist_triangle _ _ _
  toUniformSpace := UniformSpace.comap f m.toUniformSpace
  uniformity_edist := (uniformity_basis_edist.comap (Prod.map f f)).eq_biInf

/-- Pseudoemetric space instance on subsets of pseudoemetric spaces -/
instance {Î± : Type*} {p : Î± â†’ Prop} [PseudoEMetricSpace Î±] : PseudoEMetricSpace (Subtype p) :=
  PseudoEMetricSpace.induced Subtype.val â€¹_â€º

/-- The extended pseudodistance on a subset of a pseudoemetric space is the restriction of
the original pseudodistance, by definition. -/
theorem Subtype.edist_eq {p : Î± â†’ Prop} (x y : Subtype p) : edist x y = edist (x : Î±) y := rfl

/-- The extended pseudodistance on a subtype of a pseudoemetric space is the restriction of
the original pseudodistance, by definition. -/
@[simp]
theorem Subtype.edist_mk_mk {p : Î± â†’ Prop} {x y : Î±} (hx : p x) (hy : p y) :
    edist (âŸ¨x, hxâŸ© : Subtype p) âŸ¨y, hyâŸ© = edist x y :=
  rfl

/-- Consider an extended distance on a topological space, for which the neighborhoods can be
expressed in terms of the distance. Then we define the emetric space structure associated to this
distance, with a topology defeq to the initial one. -/
@[reducible] noncomputable def PseudoEmetricSpace.ofEdistOfTopology {Î± : Type*} [TopologicalSpace Î±]
    (d : Î± â†’ Î± â†’ â„â‰¥0âˆ) (h_self : âˆ€ x, d x x = 0) (h_comm : âˆ€ x y, d x y = d y x)
    (h_triangle : âˆ€ x y z, d x z â‰¤ d x y + d y z)
    (h_basis : âˆ€ x, (ğ“ x).HasBasis (fun c â†¦ 0 < c) (fun c â†¦ {y | d x y < c})) :
    PseudoEMetricSpace Î± where
  edist := d
  edist_self := h_self
  edist_comm := h_comm
  edist_triangle := h_triangle
  toUniformSpace := uniformSpaceOfEDistOfHasBasis d h_self h_comm h_triangle h_basis
  uniformity_edist := rfl

namespace MulOpposite

/-- Pseudoemetric space instance on the multiplicative opposite of a pseudoemetric space. -/
@[to_additive
/-- Pseudoemetric space instance on the additive opposite of a pseudoemetric space. -/]
instance {Î± : Type*} [PseudoEMetricSpace Î±] : PseudoEMetricSpace Î±áµáµ’áµ– :=
  PseudoEMetricSpace.induced unop â€¹_â€º

@[to_additive]
theorem edist_unop (x y : Î±áµáµ’áµ–) : edist (unop x) (unop y) = edist x y := rfl

@[to_additive]
theorem edist_op (x y : Î±) : edist (op x) (op y) = edist x y := rfl

end MulOpposite

section ULift

instance : PseudoEMetricSpace (ULift Î±) := PseudoEMetricSpace.induced ULift.down â€¹_â€º

theorem ULift.edist_eq (x y : ULift Î±) : edist x y = edist x.down y.down := rfl

@[simp]
theorem ULift.edist_up_up (x y : Î±) : edist (ULift.up x) (ULift.up y) = edist x y := rfl

end ULift

/-- The product of two pseudoemetric spaces, with the max distance, is an extended
pseudometric spaces. We make sure that the uniform structure thus constructed is the one
corresponding to the product of uniform spaces, to avoid diamond problems. -/
instance Prod.pseudoEMetricSpaceMax [PseudoEMetricSpace Î²] :
    PseudoEMetricSpace (Î± Ã— Î²) where
  edist x y := edist x.1 y.1 âŠ” edist x.2 y.2
  edist_self x := by simp
  edist_comm x y := by simp [edist_comm]
  edist_triangle _ _ _ :=
    max_le (le_trans (edist_triangle _ _ _) (add_le_add (le_max_left _ _) (le_max_left _ _)))
      (le_trans (edist_triangle _ _ _) (add_le_add (le_max_right _ _) (le_max_right _ _)))
  uniformity_edist := uniformity_prod.trans <| by
    simp [PseudoEMetricSpace.uniformity_edist, â† iInf_inf_eq, setOf_and]
  toUniformSpace := inferInstance

theorem Prod.edist_eq [PseudoEMetricSpace Î²] (x y : Î± Ã— Î²) :
    edist x y = max (edist x.1 y.1) (edist x.2 y.2) :=
  rfl

namespace EMetric

variable {x y z : Î±} {Îµ Îµâ‚ Îµâ‚‚ : â„â‰¥0âˆ} {s t : Set Î±}

/-- `EMetric.ball x Îµ` is the set of all points `y` with `edist y x < Îµ` -/
def ball (x : Î±) (Îµ : â„â‰¥0âˆ) : Set Î± :=
  { y | edist y x < Îµ }

@[simp] theorem mem_ball : y âˆˆ ball x Îµ â†” edist y x < Îµ := Iff.rfl

theorem mem_ball' : y âˆˆ ball x Îµ â†” edist x y < Îµ := by rw [edist_comm, mem_ball]

/-- `EMetric.closedBall x Îµ` is the set of all points `y` with `edist y x â‰¤ Îµ` -/
def closedBall (x : Î±) (Îµ : â„â‰¥0âˆ) :=
  { y | edist y x â‰¤ Îµ }

@[simp] theorem mem_closedBall : y âˆˆ closedBall x Îµ â†” edist y x â‰¤ Îµ := Iff.rfl

theorem mem_closedBall' : y âˆˆ closedBall x Îµ â†” edist x y â‰¤ Îµ := by rw [edist_comm, mem_closedBall]

@[simp]
theorem closedBall_top (x : Î±) : closedBall x âˆ = univ :=
  eq_univ_of_forall fun _ => mem_setOf.2 le_top

theorem ball_subset_closedBall : ball x Îµ âŠ† closedBall x Îµ := fun _ h => le_of_lt h.out

theorem pos_of_mem_ball (hy : y âˆˆ ball x Îµ) : 0 < Îµ :=
  lt_of_le_of_lt (zero_le _) hy

theorem mem_ball_self (h : 0 < Îµ) : x âˆˆ ball x Îµ := by
  rwa [mem_ball, edist_self]

theorem mem_closedBall_self : x âˆˆ closedBall x Îµ := by
  rw [mem_closedBall, edist_self]; apply zero_le

theorem mem_ball_comm : x âˆˆ ball y Îµ â†” y âˆˆ ball x Îµ := by rw [mem_ball', mem_ball]

theorem mem_closedBall_comm : x âˆˆ closedBall y Îµ â†” y âˆˆ closedBall x Îµ := by
  rw [mem_closedBall', mem_closedBall]

@[gcongr]
theorem ball_subset_ball (h : Îµâ‚ â‰¤ Îµâ‚‚) : ball x Îµâ‚ âŠ† ball x Îµâ‚‚ := fun _y (yx : _ < Îµâ‚) =>
  lt_of_lt_of_le yx h

@[gcongr]
theorem closedBall_subset_closedBall (h : Îµâ‚ â‰¤ Îµâ‚‚) : closedBall x Îµâ‚ âŠ† closedBall x Îµâ‚‚ :=
  fun _y (yx : _ â‰¤ Îµâ‚) => le_trans yx h

theorem ball_disjoint (h : Îµâ‚ + Îµâ‚‚ â‰¤ edist x y) : Disjoint (ball x Îµâ‚) (ball y Îµâ‚‚) :=
  Set.disjoint_left.mpr fun z hâ‚ hâ‚‚ =>
    (edist_triangle_left x y z).not_gt <| (ENNReal.add_lt_add hâ‚ hâ‚‚).trans_le h

theorem ball_subset (h : edist x y + Îµâ‚ â‰¤ Îµâ‚‚) (h' : edist x y â‰  âˆ) : ball x Îµâ‚ âŠ† ball y Îµâ‚‚ :=
  fun z zx =>
  calc
    edist z y â‰¤ edist z x + edist x y := edist_triangle _ _ _
    _ = edist x y + edist z x := add_comm _ _
    _ < edist x y + Îµâ‚ := ENNReal.add_lt_add_left h' zx
    _ â‰¤ Îµâ‚‚ := h

theorem exists_ball_subset_ball (h : y âˆˆ ball x Îµ) : âˆƒ Îµ' > 0, ball y Îµ' âŠ† ball x Îµ := by
  have : 0 < Îµ - edist y x := by simpa using h
  refine âŸ¨Îµ - edist y x, this, ball_subset ?_ (ne_top_of_lt h)âŸ©
  exact (add_tsub_cancel_of_le (mem_ball.mp h).le).le

theorem ball_eq_empty_iff : ball x Îµ = âˆ… â†” Îµ = 0 :=
  eq_empty_iff_forall_notMem.trans
    âŸ¨fun h => le_bot_iff.1 (le_of_not_gt fun Îµ0 => h _ (mem_ball_self Îµ0)), fun Îµ0 _ h =>
      not_lt_of_ge (le_of_eq Îµ0) (pos_of_mem_ball h)âŸ©

theorem ordConnected_setOf_closedBall_subset (x : Î±) (s : Set Î±) :
    OrdConnected { r | closedBall x r âŠ† s } :=
  âŸ¨fun _ _ _ hâ‚ _ hâ‚‚ => (closedBall_subset_closedBall hâ‚‚.2).trans hâ‚âŸ©

theorem ordConnected_setOf_ball_subset (x : Î±) (s : Set Î±) : OrdConnected { r | ball x r âŠ† s } :=
  âŸ¨fun _ _ _ hâ‚ _ hâ‚‚ => (ball_subset_ball hâ‚‚.2).trans hâ‚âŸ©

/-- Relation â€œtwo points are at a finite edistanceâ€ is an equivalence relation. -/
def edistLtTopSetoid : Setoid Î± where
  r x y := edist x y < âŠ¤
  iseqv :=
    âŸ¨fun x => by rw [edist_self]; exact ENNReal.coe_lt_top,
      fun h => by rwa [edist_comm], fun hxy hyz =>
        lt_of_le_of_lt (edist_triangle _ _ _) (ENNReal.add_lt_top.2 âŸ¨hxy, hyzâŸ©)âŸ©

@[simp]
theorem ball_zero : ball x 0 = âˆ… := by rw [EMetric.ball_eq_empty_iff]

theorem nhds_basis_eball : (ğ“ x).HasBasis (fun Îµ : â„â‰¥0âˆ => 0 < Îµ) (ball x) :=
  nhds_basis_uniformity uniformity_basis_edist

theorem nhdsWithin_basis_eball : (ğ“[s] x).HasBasis (fun Îµ : â„â‰¥0âˆ => 0 < Îµ) fun Îµ => ball x Îµ âˆ© s :=
  nhdsWithin_hasBasis nhds_basis_eball s

theorem nhds_basis_closed_eball : (ğ“ x).HasBasis (fun Îµ : â„â‰¥0âˆ => 0 < Îµ) (closedBall x) :=
  nhds_basis_uniformity uniformity_basis_edist_le

theorem nhdsWithin_basis_closed_eball :
    (ğ“[s] x).HasBasis (fun Îµ : â„â‰¥0âˆ => 0 < Îµ) fun Îµ => closedBall x Îµ âˆ© s :=
  nhdsWithin_hasBasis nhds_basis_closed_eball s

theorem nhds_eq : ğ“ x = â¨… Îµ > 0, ğ“Ÿ (ball x Îµ) :=
  nhds_basis_eball.eq_biInf

theorem mem_nhds_iff : s âˆˆ ğ“ x â†” âˆƒ Îµ > 0, ball x Îµ âŠ† s :=
  nhds_basis_eball.mem_iff

theorem mem_nhdsWithin_iff : s âˆˆ ğ“[t] x â†” âˆƒ Îµ > 0, ball x Îµ âˆ© t âŠ† s :=
  nhdsWithin_basis_eball.mem_iff

section

variable [PseudoEMetricSpace Î²] {f : Î± â†’ Î²}

theorem tendsto_nhdsWithin_nhdsWithin {t : Set Î²} {a b} :
    Tendsto f (ğ“[s] a) (ğ“[t] b) â†”
      âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ edist x a < Î´ â†’ f x âˆˆ t âˆ§ edist (f x) b < Îµ :=
  (nhdsWithin_basis_eball.tendsto_iff nhdsWithin_basis_eball).trans <|
    forallâ‚‚_congr fun Îµ _ => exists_congr fun Î´ => and_congr_right fun _ =>
      forall_congr' fun x => by simp; tauto

theorem tendsto_nhdsWithin_nhds {a b} :
    Tendsto f (ğ“[s] a) (ğ“ b) â†”
      âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ {x : Î±}, x âˆˆ s â†’ edist x a < Î´ â†’ edist (f x) b < Îµ := by
  rw [â† nhdsWithin_univ b, tendsto_nhdsWithin_nhdsWithin]
  simp only [mem_univ, true_and]

theorem tendsto_nhds_nhds {a b} :
    Tendsto f (ğ“ a) (ğ“ b) â†” âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ â¦ƒxâ¦„, edist x a < Î´ â†’ edist (f x) b < Îµ :=
  nhds_basis_eball.tendsto_iff nhds_basis_eball

end

theorem isOpen_iff : IsOpen s â†” âˆ€ x âˆˆ s, âˆƒ Îµ > 0, ball x Îµ âŠ† s := by
  simp [isOpen_iff_nhds, mem_nhds_iff]

@[simp] theorem isOpen_ball : IsOpen (ball x Îµ) :=
  isOpen_iff.2 fun _ => exists_ball_subset_ball

theorem isClosed_ball_top : IsClosed (ball x âŠ¤) :=
  isOpen_compl_iff.1 <| isOpen_iff.2 fun _y hy =>
    âŸ¨âŠ¤, ENNReal.coe_lt_top, fun _z hzy hzx =>
      hy (edistLtTopSetoid.trans (edistLtTopSetoid.symm hzy) hzx)âŸ©

theorem ball_mem_nhds (x : Î±) {Îµ : â„â‰¥0âˆ} (Îµ0 : 0 < Îµ) : ball x Îµ âˆˆ ğ“ x :=
  isOpen_ball.mem_nhds (mem_ball_self Îµ0)

theorem closedBall_mem_nhds (x : Î±) {Îµ : â„â‰¥0âˆ} (Îµ0 : 0 < Îµ) : closedBall x Îµ âˆˆ ğ“ x :=
  mem_of_superset (ball_mem_nhds x Îµ0) ball_subset_closedBall

theorem ball_prod_same [PseudoEMetricSpace Î²] (x : Î±) (y : Î²) (r : â„â‰¥0âˆ) :
    ball x r Ã—Ë¢ ball y r = ball (x, y) r :=
  ext fun z => by simp [Prod.edist_eq]

theorem closedBall_prod_same [PseudoEMetricSpace Î²] (x : Î±) (y : Î²) (r : â„â‰¥0âˆ) :
    closedBall x r Ã—Ë¢ closedBall y r = closedBall (x, y) r :=
  ext fun z => by simp [Prod.edist_eq]

/-- Îµ-characterization of the closure in pseudoemetric spaces -/
theorem mem_closure_iff : x âˆˆ closure s â†” âˆ€ Îµ > 0, âˆƒ y âˆˆ s, edist x y < Îµ :=
  (mem_closure_iff_nhds_basis nhds_basis_eball).trans <| by simp only [mem_ball, edist_comm x]

theorem tendsto_nhds {f : Filter Î²} {u : Î² â†’ Î±} {a : Î±} :
    Tendsto u f (ğ“ a) â†” âˆ€ Îµ > 0, âˆ€á¶  x in f, edist (u x) a < Îµ :=
  nhds_basis_eball.tendsto_right_iff

theorem tendsto_atTop [Nonempty Î²] [SemilatticeSup Î²] {u : Î² â†’ Î±} {a : Î±} :
    Tendsto u atTop (ğ“ a) â†” âˆ€ Îµ > 0, âˆƒ N, âˆ€ n â‰¥ N, edist (u n) a < Îµ :=
  (atTop_basis.tendsto_iff nhds_basis_eball).trans <| by
    simp only [true_and, mem_Ici, mem_ball]

section Compact

-- TODO: generalize to a uniform space with metrizable uniformity
/-- For a set `s` in a pseudo emetric space, if for every `Îµ > 0` there exists a countable
set that is `Îµ`-dense in `s`, then there exists a countable subset `t âŠ† s` that is dense in `s`. -/
theorem subset_countable_closure_of_almost_dense_set (s : Set Î±)
    (hs : âˆ€ Îµ > 0, âˆƒ t : Set Î±, t.Countable âˆ§ s âŠ† â‹ƒ x âˆˆ t, closedBall x Îµ) :
    âˆƒ t, t âŠ† s âˆ§ t.Countable âˆ§ s âŠ† closure t := by
  rcases s.eq_empty_or_nonempty with (rfl | âŸ¨xâ‚€, hxâ‚€âŸ©)
  Â· exact âŸ¨âˆ…, empty_subset _, countable_empty, empty_subset _âŸ©
  choose! T hTc hsT using fun n : â„• => hs nâ»Â¹ (by simp)
  have : âˆ€ r x, âˆƒ y âˆˆ s, closedBall x r âˆ© s âŠ† closedBall y (r * 2) := fun r x => by
    rcases (closedBall x r âˆ© s).eq_empty_or_nonempty with (he | âŸ¨y, hxy, hysâŸ©)
    Â· refine âŸ¨xâ‚€, hxâ‚€, ?_âŸ©
      rw [he]
      exact empty_subset _
    Â· refine âŸ¨y, hys, fun z hz => ?_âŸ©
      calc
        edist z y â‰¤ edist z x + edist y x := edist_triangle_right _ _ _
        _ â‰¤ r + r := add_le_add hz.1 hxy
        _ = r * 2 := (mul_two r).symm
  choose f hfs hf using this
  refine
    âŸ¨â‹ƒ n : â„•, f nâ»Â¹ '' T n, iUnion_subset fun n => image_subset_iff.2 fun z _ => hfs _ _,
      countable_iUnion fun n => (hTc n).image _, ?_âŸ©
  refine fun x hx => mem_closure_iff.2 fun Îµ Îµ0 => ?_
  rcases ENNReal.exists_inv_nat_lt (ENNReal.half_pos Îµ0.lt.ne').ne' with âŸ¨n, hnâŸ©
  rcases mem_iUnionâ‚‚.1 (hsT n hx) with âŸ¨y, hyn, hyxâŸ©
  refine âŸ¨f nâ»Â¹ y, mem_iUnion.2 âŸ¨n, mem_image_of_mem _ hynâŸ©, ?_âŸ©
  calc
    edist x (f nâ»Â¹ y) â‰¤ (n : â„â‰¥0âˆ)â»Â¹ * 2 := hf _ _ âŸ¨hyx, hxâŸ©
    _ < Îµ := ENNReal.mul_lt_of_lt_div hn

end Compact

end EMetric

/-- An extended metric space is a type endowed with a `â„â‰¥0âˆ`-valued distance `edist` satisfying
`edist x y = 0 â†” x = y`, commutativity `edist x y = edist y x`, and the triangle inequality
`edist x z â‰¤ edist x y + edist y z`.

See pseudo extended metric spaces (`PseudoEMetricSpace`) for the similar class with the
`edist x y = 0 â†” x = y` assumption weakened to `edist x x = 0`.

Any extended metric space is a T1 topological space and a uniform space (see `TopologicalSpace`,
`T1Space`, `UniformSpace`), where the topology and uniformity come from the metric.

We make the uniformity/topology part of the data instead of deriving it from the metric.
This eg ensures that we do not get a diamond when doing
`[EMetricSpace Î±] [EMetricSpace Î²] : TopologicalSpace (Î± Ã— Î²)`:
The product metric and product topology agree, but not definitionally so.
See Note [forgetful inheritance]. -/
class EMetricSpace (Î± : Type u) : Type u extends PseudoEMetricSpace Î± where
  eq_of_edist_eq_zero : âˆ€ {x y : Î±}, edist x y = 0 â†’ x = y

@[ext]
protected theorem EMetricSpace.ext
    {Î± : Type*} {m m' : EMetricSpace Î±} (h : m.toEDist = m'.toEDist) : m = m' := by
  cases m
  cases m'
  congr
  ext1
  assumption

variable {Î³ : Type w} [EMetricSpace Î³]

export EMetricSpace (eq_of_edist_eq_zero)

/-- Characterize the equality of points by the vanishing of their extended distance -/
@[simp]
theorem edist_eq_zero {x y : Î³} : edist x y = 0 â†” x = y :=
  âŸ¨eq_of_edist_eq_zero, fun h => h â–¸ edist_self _âŸ©

@[simp]
theorem zero_eq_edist {x y : Î³} : 0 = edist x y â†” x = y := eq_comm.trans edist_eq_zero

theorem edist_le_zero {x y : Î³} : edist x y â‰¤ 0 â†” x = y :=
  nonpos_iff_eq_zero.trans edist_eq_zero

@[simp]
theorem edist_pos {x y : Î³} : 0 < edist x y â†” x â‰  y := by simp [â† not_le]

@[simp] lemma EMetric.closedBall_zero (x : Î³) : closedBall x 0 = {x} := by ext; simp

/-- Two points coincide if their distance is `< Îµ` for all positive Îµ -/
theorem eq_of_forall_edist_le {x y : Î³} (h : âˆ€ Îµ > 0, edist x y â‰¤ Îµ) : x = y :=
  eq_of_edist_eq_zero (eq_of_le_of_forall_lt_imp_le_of_dense bot_le h)

/-- Auxiliary function to replace the uniformity on an emetric space with
a uniformity which is equal to the original one, but maybe not defeq.
This is useful if one wants to construct an emetric space with a
specified uniformity. See Note [forgetful inheritance] explaining why having definitionally
the right uniformity is often important.
See note [reducible non-instances].
-/
abbrev EMetricSpace.replaceUniformity {Î³} [U : UniformSpace Î³] (m : EMetricSpace Î³)
    (H : ğ“¤[U] = ğ“¤[PseudoEMetricSpace.toUniformSpace]) : EMetricSpace Î³ where
  edist := @edist _ m.toEDist
  edist_self := edist_self
  eq_of_edist_eq_zero := @eq_of_edist_eq_zero _ _
  edist_comm := edist_comm
  edist_triangle := edist_triangle
  toUniformSpace := U
  uniformity_edist := H.trans (@PseudoEMetricSpace.uniformity_edist Î³ _)

/-- Auxiliary function to replace the topology on an emetric space with
a topology which is equal to the original one, but maybe not defeq.
This is useful if one wants to construct an emetric space with a
specified topology. See Note [forgetful inheritance] explaining why having definitionally
the right topology is often important.
See note [reducible non-instances].
-/
abbrev EMetricSpace.replaceTopology {Î³} [T : TopologicalSpace Î³] (m : EMetricSpace Î³)
    (H : T = m.toUniformSpace.toTopologicalSpace) : EMetricSpace Î³ where
  edist := @edist _ m.toEDist
  edist_self := edist_self
  eq_of_edist_eq_zero := @eq_of_edist_eq_zero _ _
  edist_comm := edist_comm
  edist_triangle := edist_triangle
  toUniformSpace := m.toUniformSpace.replaceTopology H
  uniformity_edist := PseudoEMetricSpace.uniformity_edist

/-- The extended metric induced by an injective function taking values in an emetric space.
See Note [reducible non-instances]. -/
abbrev EMetricSpace.induced {Î³ Î²} (f : Î³ â†’ Î²) (hf : Function.Injective f) (m : EMetricSpace Î²) :
    EMetricSpace Î³ :=
  { PseudoEMetricSpace.induced f m.toPseudoEMetricSpace with
    eq_of_edist_eq_zero := fun h => hf (edist_eq_zero.1 h) }

/-- EMetric space instance on subsets of emetric spaces -/
instance {Î± : Type*} {p : Î± â†’ Prop} [EMetricSpace Î±] : EMetricSpace (Subtype p) :=
  EMetricSpace.induced Subtype.val Subtype.coe_injective â€¹_â€º

/-- EMetric space instance on the multiplicative opposite of an emetric space. -/
@[to_additive /-- EMetric space instance on the additive opposite of an emetric space. -/]
instance {Î± : Type*} [EMetricSpace Î±] : EMetricSpace Î±áµáµ’áµ– :=
  EMetricSpace.induced MulOpposite.unop MulOpposite.unop_injective â€¹_â€º

instance {Î± : Type*} [EMetricSpace Î±] : EMetricSpace (ULift Î±) :=
  EMetricSpace.induced ULift.down ULift.down_injective â€¹_â€º

/-- Reformulation of the uniform structure in terms of the extended distance -/
theorem uniformity_edist : ğ“¤ Î³ = â¨… Îµ > 0, ğ“Ÿ { p : Î³ Ã— Î³ | edist p.1 p.2 < Îµ } :=
  PseudoEMetricSpace.uniformity_edist

/-!
### `Additive`, `Multiplicative`

The distance on those type synonyms is inherited without change.
-/


open Additive Multiplicative

section

variable [EDist X]

instance : EDist (Additive X) := â€¹EDist Xâ€º
instance : EDist (Multiplicative X) := â€¹EDist Xâ€º

@[simp]
theorem edist_ofMul (a b : X) : edist (ofMul a) (ofMul b) = edist a b :=
  rfl

@[simp]
theorem edist_ofAdd (a b : X) : edist (ofAdd a) (ofAdd b) = edist a b :=
  rfl

@[simp]
theorem edist_toMul (a b : Additive X) : edist a.toMul b.toMul = edist a b :=
  rfl

@[simp]
theorem edist_toAdd (a b : Multiplicative X) : edist a.toAdd b.toAdd = edist a b :=
  rfl

end

instance [PseudoEMetricSpace X] : PseudoEMetricSpace (Additive X) := â€¹PseudoEMetricSpace Xâ€º
instance [PseudoEMetricSpace X] : PseudoEMetricSpace (Multiplicative X) := â€¹PseudoEMetricSpace Xâ€º
instance [EMetricSpace X] : EMetricSpace (Additive X) := â€¹EMetricSpace Xâ€º
instance [EMetricSpace X] : EMetricSpace (Multiplicative X) := â€¹EMetricSpace Xâ€º

/-!
### Order dual

The distance on this type synonym is inherited without change.
-/


open OrderDual

section

variable [EDist X]

instance : EDist Xáµ’áµˆ := â€¹EDist Xâ€º

@[simp]
theorem edist_toDual (a b : X) : edist (toDual a) (toDual b) = edist a b :=
  rfl

@[simp]
theorem edist_ofDual (a b : Xáµ’áµˆ) : edist (ofDual a) (ofDual b) = edist a b :=
  rfl

end

instance [PseudoEMetricSpace X] : PseudoEMetricSpace Xáµ’áµˆ := â€¹PseudoEMetricSpace Xâ€º
instance [EMetricSpace X] : EMetricSpace Xáµ’áµˆ := â€¹EMetricSpace Xâ€º
