/-
Copyright (c) 2015 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Robert Y. Lewis, Johannes H√∂lzl, Mario Carneiro, S√©bastien Gou√´zel
-/
module

public import Mathlib.Data.ENNReal.Inv
public import Mathlib.Topology.UniformSpace.Basic
public import Mathlib.Topology.UniformSpace.OfFun

/-!
# Extended metric spaces

This file is devoted to the definition and study of `EMetricSpace`s, i.e., metric
spaces in which the distance is allowed to take the value ‚àû. This extended distance is
called `edist`, and takes values in `‚Ñù‚â•0‚àû`.

Many definitions and theorems expected on emetric spaces are already introduced on uniform spaces
and topological spaces. For example: open and closed sets, compactness, completeness, continuity and
uniform continuity.

The class `EMetricSpace` therefore extends `UniformSpace` (and `TopologicalSpace`).

Since a lot of elementary properties don't require `eq_of_edist_eq_zero` we start setting up the
theory of `PseudoEMetricSpace`, where we don't require `edist x y = 0 ‚Üí x = y` and we specialize
to `EMetricSpace` at the end.
-/

@[expose] public section


assert_not_exists Nat.instLocallyFiniteOrder IsUniformEmbedding TendstoUniformlyOnFilter

open Filter Set Topology Set.Notation

universe u v w

variable {Œ± : Type u} {Œ≤ : Type v} {X : Type*}

/-- Characterizing uniformities associated to a (generalized) distance function `D`
in terms of the elements of the uniformity. -/
theorem uniformity_dist_of_mem_uniformity [LT Œ≤] {U : Filter (Œ± √ó Œ±)} (z : Œ≤)
    (D : Œ± ‚Üí Œ± ‚Üí Œ≤) (H : ‚àÄ s, s ‚àà U ‚Üî ‚àÉ Œµ > z, ‚àÄ {a b : Œ±}, D a b < Œµ ‚Üí (a, b) ‚àà s) :
    U = ‚®Ö Œµ > z, ùìü { p : Œ± √ó Œ± | D p.1 p.2 < Œµ } :=
  HasBasis.eq_biInf ‚ü®fun s => by simp only [H, subset_def, Prod.forall, mem_setOf]‚ü©

open scoped Uniformity Topology Filter NNReal ENNReal Pointwise

/-- `EDist Œ±` means that `Œ±` is equipped with an extended distance. -/
@[ext]
class EDist (Œ± : Type*) where
  /-- Extended distance between two points -/
  edist : Œ± ‚Üí Œ± ‚Üí ‚Ñù‚â•0‚àû

export EDist (edist)

section

variable {x y z : Œ±} {Œµ Œµ‚ÇÅ Œµ‚ÇÇ : ‚Ñù‚â•0‚àû} {s t : Set Œ±} [EDist Œ±]

/-- `EMetric.ball x Œµ` is the set of all points `y` with `edist y x < Œµ` -/
def EMetric.ball (x : Œ±) (Œµ : ‚Ñù‚â•0‚àû) : Set Œ± :=
  { y | edist y x < Œµ }

@[simp] theorem EMetric.mem_ball : y ‚àà ball x Œµ ‚Üî edist y x < Œµ := Iff.rfl

end

/-- Creating a uniform space from an extended distance. -/
@[reducible] def uniformSpaceOfEDist (edist : Œ± ‚Üí Œ± ‚Üí ‚Ñù‚â•0‚àû) (edist_self : ‚àÄ x : Œ±, edist x x = 0)
    (edist_comm : ‚àÄ x y : Œ±, edist x y = edist y x)
    (edist_triangle : ‚àÄ x y z : Œ±, edist x z ‚â§ edist x y + edist y z) : UniformSpace Œ± :=
  .ofFun edist edist_self edist_comm edist_triangle fun Œµ Œµ0 =>
    ‚ü®Œµ / 2, ENNReal.half_pos Œµ0.ne', fun _ h‚ÇÅ _ h‚ÇÇ =>
      (ENNReal.add_lt_add h‚ÇÅ h‚ÇÇ).trans_eq (ENNReal.add_halves _)‚ü©

/-- Creating a uniform space from an extended distance. We assume that
there is a preexisting topology, for which the neighborhoods can be expressed using the distance,
and we make sure that the uniform space structure we construct has a topology which is defeq
to the original one. -/
@[reducible] noncomputable def uniformSpaceOfEDistOfHasBasis [TopologicalSpace Œ±]
    (edist : Œ± ‚Üí Œ± ‚Üí ‚Ñù‚â•0‚àû)
    (edist_self : ‚àÄ x : Œ±, edist x x = 0)
    (edist_comm : ‚àÄ x y : Œ±, edist x y = edist y x)
    (edist_triangle : ‚àÄ x y z : Œ±, edist x z ‚â§ edist x y + edist y z)
    (basis : ‚àÄ x, (ùìù x).HasBasis (fun c ‚Ü¶ 0 < c) (fun c ‚Ü¶ {y | edist x y < c})) :
    UniformSpace Œ± :=
  .ofFunOfHasBasis edist edist_self edist_comm edist_triangle (fun Œµ Œµ0 =>
    ‚ü®Œµ / 2, ENNReal.half_pos Œµ0.ne', fun _ h‚ÇÅ _ h‚ÇÇ =>
      (ENNReal.add_lt_add h‚ÇÅ h‚ÇÇ).trans_eq (ENNReal.add_halves _)‚ü©) basis

/-- A pseudo extended metric space is a type endowed with a `‚Ñù‚â•0‚àû`-valued distance `edist`
satisfying reflexivity `edist x x = 0`, commutativity `edist x y = edist y x`, and the triangle
inequality `edist x z ‚â§ edist x y + edist y z`.

Note that we do not require `edist x y = 0 ‚Üí x = y`. See extended metric spaces (`EMetricSpace`) for
the similar class with that stronger assumption.

Any pseudo extended metric space is a topological space and a uniform space (see `TopologicalSpace`,
`UniformSpace`), where the topology and uniformity come from the metric.
Note that a T1 pseudo extended metric space is just an extended metric space.

We make the uniformity/topology part of the data instead of deriving it from the metric. This e.g.
ensures that we do not get a diamond when doing
`[PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] : TopologicalSpace (Œ± √ó Œ≤)`:
The product metric and product topology agree, but not definitionally so.
See Note [forgetful inheritance]. -/
class PseudoEMetricSpace (Œ± : Type u) : Type u extends EDist Œ± where
  edist_self : ‚àÄ x : Œ±, edist x x = 0
  edist_comm : ‚àÄ x y : Œ±, edist x y = edist y x
  edist_triangle : ‚àÄ x y z : Œ±, edist x z ‚â§ edist x y + edist y z
  toUniformSpace : UniformSpace Œ± := uniformSpaceOfEDist edist edist_self edist_comm edist_triangle
  uniformity_edist : ùì§ Œ± = ‚®Ö Œµ > 0, ùìü { p : Œ± √ó Œ± | edist p.1 p.2 < Œµ } := by rfl

attribute [instance] PseudoEMetricSpace.toUniformSpace

/- Pseudoemetric spaces are less common than metric spaces. Therefore, we work in a dedicated
namespace, while notions associated to metric spaces are mostly in the root namespace. -/

/-- Two pseudo emetric space structures with the same edistance function coincide. -/
@[ext]
protected theorem PseudoEMetricSpace.ext {Œ± : Type*} {m m' : PseudoEMetricSpace Œ±}
    (h : m.toEDist = m'.toEDist) : m = m' := by
  obtain ‚ü®_, _, _, U, hU‚ü© := m; rename EDist Œ± => ed
  obtain ‚ü®_, _, _, U', hU'‚ü© := m'; rename EDist Œ± => ed'
  congr 1
  exact UniformSpace.ext (((show ed = ed' from h) ‚ñ∏ hU).trans hU'.symm)

variable [PseudoEMetricSpace Œ±]

export PseudoEMetricSpace (edist_self edist_comm edist_triangle)

attribute [simp] edist_self

/-- Triangle inequality for the extended distance -/
theorem edist_triangle_left (x y z : Œ±) : edist x y ‚â§ edist z x + edist z y := by
  rw [edist_comm z]; apply edist_triangle

theorem edist_triangle_right (x y z : Œ±) : edist x y ‚â§ edist x z + edist y z := by
  rw [edist_comm y]; apply edist_triangle

theorem edist_congr_right {x y z : Œ±} (h : edist x y = 0) : edist x z = edist y z := by
  apply le_antisymm
  ¬∑ rw [‚Üê zero_add (edist y z), ‚Üê h]
    apply edist_triangle
  ¬∑ rw [edist_comm] at h
    rw [‚Üê zero_add (edist x z), ‚Üê h]
    apply edist_triangle

theorem edist_congr_left {x y z : Œ±} (h : edist x y = 0) : edist z x = edist z y := by
  rw [edist_comm z x, edist_comm z y]
  apply edist_congr_right h

theorem edist_congr {w x y z : Œ±} (hl : edist w x = 0) (hr : edist y z = 0) :
    edist w y = edist x z :=
  (edist_congr_right hl).trans (edist_congr_left hr)

theorem edist_triangle4 (x y z t : Œ±) : edist x t ‚â§ edist x y + edist y z + edist z t := by
  grw [edist_triangle _ z, edist_triangle]

/-- Reformulation of the uniform structure in terms of the extended distance -/
theorem uniformity_pseudoedist : ùì§ Œ± = ‚®Ö Œµ > 0, ùìü { p : Œ± √ó Œ± | edist p.1 p.2 < Œµ } :=
  PseudoEMetricSpace.uniformity_edist

theorem uniformSpace_edist :
    ‚ÄπPseudoEMetricSpace Œ±‚Ä∫.toUniformSpace =
      uniformSpaceOfEDist edist edist_self edist_comm edist_triangle :=
  UniformSpace.ext uniformity_pseudoedist

theorem uniformity_basis_edist :
    (ùì§ Œ±).HasBasis (fun Œµ : ‚Ñù‚â•0‚àû => 0 < Œµ) fun Œµ => { p : Œ± √ó Œ± | edist p.1 p.2 < Œµ } :=
  (@uniformSpace_edist Œ± _).symm ‚ñ∏ UniformSpace.hasBasis_ofFun ‚ü®1, one_pos‚ü© _ _ _ _ _

/-- Characterization of the elements of the uniformity in terms of the extended distance -/
theorem mem_uniformity_edist {s : Set (Œ± √ó Œ±)} :
    s ‚àà ùì§ Œ± ‚Üî ‚àÉ Œµ > 0, ‚àÄ {a b : Œ±}, edist a b < Œµ ‚Üí (a, b) ‚àà s :=
  uniformity_basis_edist.mem_uniformity_iff

/-- Make a PseudoEMetricSpace from the metric. -/
abbrev pseudoEMetricSpaceOfEDist {Œ± : Type u} [EDist Œ±] (edist_self : ‚àÄ x : Œ±, edist x x = 0)
    (edist_comm : ‚àÄ x y : Œ±, edist x y = edist y x)
    (edist_triangle : ‚àÄ x y z : Œ±, edist x z ‚â§ edist x y + edist y z) : PseudoEMetricSpace Œ± where
  edist := edist
  edist_self := edist_self
  edist_comm := edist_comm
  edist_triangle := edist_triangle
  toUniformSpace := uniformSpaceOfEDist edist edist_self edist_comm edist_triangle
  uniformity_edist := by rfl

/-- Make a PseudoEMetricSpace from the metric. -/
abbrev pseudoEMetricSpaceOfEDist'
    {Œ± : Type u} (edist : Œ± ‚Üí Œ± ‚Üí ‚Ñù‚â•0‚àû) (edist_self : ‚àÄ x : Œ±, edist x x = 0)
    (edist_comm : ‚àÄ x y : Œ±, edist x y = edist y x) (edist_triangle : ‚àÄ x y z :
    Œ±, edist x z ‚â§ edist x y + edist y z) : PseudoEMetricSpace Œ± where
  edist := edist
  edist_self := edist_self
  edist_comm := edist_comm
  edist_triangle := edist_triangle
  toUniformSpace := uniformSpaceOfEDist edist edist_self edist_comm edist_triangle
  uniformity_edist := by rfl

theorem toPseudoEMetricSpaceToUniformSpace_uniformSpaceOfEDist'
    {Œ± : Type u} [TopologicalSpace Œ±] (edist : Œ± ‚Üí Œ± ‚Üí ‚Ñù‚â•0‚àû) (edist_self : ‚àÄ x : Œ±, edist x x = 0)
    (edist_comm : ‚àÄ x y : Œ±, edist x y = edist y x)
    (edist_triangle : ‚àÄ x y z : Œ±, edist x z ‚â§ edist x y + edist y z) :
    ((pseudoEMetricSpaceOfEDist'
    edist edist_self edist_comm edist_triangle).toUniformSpace).toTopologicalSpace =
    ((uniformSpaceOfEDist
    edist edist_self edist_comm edist_triangle)).toTopologicalSpace := by rfl

theorem toPseudoEMetricSpaceToUniformSpace_uniformSpaceOfEDist_congr'
    {Œ± : Type u} [TopologicalSpace Œ±] (s : Set Œ±) (edist : Œ± ‚Üí Œ± ‚Üí ‚Ñù‚â•0‚àû)
    (edist_self : ‚àÄ x : Œ±, edist x x = 0) (edist_comm : ‚àÄ x y : Œ±, edist x y = edist y x)
    (edist_triangle : ‚àÄ x y z : Œ±, edist x z ‚â§ edist x y + edist y z):
    IsOpen[((pseudoEMetricSpaceOfEDist'
    edist edist_self edist_comm edist_triangle).toUniformSpace).toTopologicalSpace] s
    ‚Üî IsOpen[((uniformSpaceOfEDist
    edist edist_self edist_comm edist_triangle)).toTopologicalSpace] s := by rfl

theorem EMetric.Uniformity_eq {Œ± : Type u} [EDist Œ±] (edist_self : ‚àÄ x : Œ±, edist x x = 0)
    (edist_comm : ‚àÄ x y : Œ±, edist x y = edist y x)
    (edist_triangle : ‚àÄ x y z : Œ±, edist x z ‚â§ edist x y + edist y z) :
    (uniformSpaceOfEDist edist edist_self edist_comm edist_triangle) =
    (pseudoEMetricSpaceOfEDist edist_self edist_comm edist_triangle).toUniformSpace := by rfl

/-- Given `f : Œ≤ ‚Üí ‚Ñù‚â•0‚àû`, if `f` sends `{i | p i}` to a set of positive numbers
accumulating to zero, then `f i`-neighborhoods of the diagonal form a basis of `ùì§ Œ±`.

For specific bases see `uniformity_basis_edist`, `uniformity_basis_edist'`,
`uniformity_basis_edist_nnreal`, and `uniformity_basis_edist_inv_nat`. -/
protected theorem EMetric.mk_uniformity_basis {Œ≤ : Type*} {p : Œ≤ ‚Üí Prop} {f : Œ≤ ‚Üí ‚Ñù‚â•0‚àû}
    (hf‚ÇÄ : ‚àÄ x, p x ‚Üí 0 < f x) (hf : ‚àÄ Œµ, 0 < Œµ ‚Üí ‚àÉ x, p x ‚àß f x ‚â§ Œµ) :
    (ùì§ Œ±).HasBasis p fun x => { p : Œ± √ó Œ± | edist p.1 p.2 < f x } := by
  refine ‚ü®fun s => uniformity_basis_edist.mem_iff.trans ?_‚ü©
  constructor
  ¬∑ rintro ‚ü®Œµ, Œµ‚ÇÄ, hŒµ‚ü©
    rcases hf Œµ Œµ‚ÇÄ with ‚ü®i, hi, H‚ü©
    exact ‚ü®i, hi, fun x hx => hŒµ <| lt_of_lt_of_le hx.out H‚ü©
  ¬∑ exact fun ‚ü®i, hi, H‚ü© => ‚ü®f i, hf‚ÇÄ i hi, H‚ü©

/-- Given `f : Œ≤ ‚Üí ‚Ñù‚â•0‚àû`, if `f` sends `{i | p i}` to a set of positive numbers
accumulating to zero, then closed `f i`-neighborhoods of the diagonal form a basis of `ùì§ Œ±`.

For specific bases see `uniformity_basis_edist_le` and `uniformity_basis_edist_le'`. -/
protected theorem EMetric.mk_uniformity_basis_le {Œ≤ : Type*} {p : Œ≤ ‚Üí Prop} {f : Œ≤ ‚Üí ‚Ñù‚â•0‚àû}
    (hf‚ÇÄ : ‚àÄ x, p x ‚Üí 0 < f x) (hf : ‚àÄ Œµ, 0 < Œµ ‚Üí ‚àÉ x, p x ‚àß f x ‚â§ Œµ) :
    (ùì§ Œ±).HasBasis p fun x => { p : Œ± √ó Œ± | edist p.1 p.2 ‚â§ f x } := by
  refine ‚ü®fun s => uniformity_basis_edist.mem_iff.trans ?_‚ü©
  constructor
  ¬∑ rintro ‚ü®Œµ, Œµ‚ÇÄ, hŒµ‚ü©
    rcases exists_between Œµ‚ÇÄ with ‚ü®Œµ', hŒµ'‚ü©
    rcases hf Œµ' hŒµ'.1 with ‚ü®i, hi, H‚ü©
    exact ‚ü®i, hi, fun x hx => hŒµ <| lt_of_le_of_lt (le_trans hx.out H) hŒµ'.2‚ü©
  ¬∑ exact fun ‚ü®i, hi, H‚ü© => ‚ü®f i, hf‚ÇÄ i hi, fun x hx => H (le_of_lt hx.out)‚ü©

theorem uniformity_basis_edist_le :
    (ùì§ Œ±).HasBasis (fun Œµ : ‚Ñù‚â•0‚àû => 0 < Œµ) fun Œµ => { p : Œ± √ó Œ± | edist p.1 p.2 ‚â§ Œµ } :=
  EMetric.mk_uniformity_basis_le (fun _ => id) fun Œµ Œµ‚ÇÄ => ‚ü®Œµ, Œµ‚ÇÄ, le_refl Œµ‚ü©

theorem uniformity_basis_edist' (Œµ' : ‚Ñù‚â•0‚àû) (hŒµ' : 0 < Œµ') :
    (ùì§ Œ±).HasBasis (fun Œµ : ‚Ñù‚â•0‚àû => Œµ ‚àà Ioo 0 Œµ') fun Œµ => { p : Œ± √ó Œ± | edist p.1 p.2 < Œµ } :=
  EMetric.mk_uniformity_basis (fun _ => And.left) fun Œµ Œµ‚ÇÄ =>
    let ‚ü®Œ¥, hŒ¥‚ü© := exists_between hŒµ'
    ‚ü®min Œµ Œ¥, ‚ü®lt_min Œµ‚ÇÄ hŒ¥.1, lt_of_le_of_lt (min_le_right _ _) hŒ¥.2‚ü©, min_le_left _ _‚ü©

theorem uniformity_basis_edist_le' (Œµ' : ‚Ñù‚â•0‚àû) (hŒµ' : 0 < Œµ') :
    (ùì§ Œ±).HasBasis (fun Œµ : ‚Ñù‚â•0‚àû => Œµ ‚àà Ioo 0 Œµ') fun Œµ => { p : Œ± √ó Œ± | edist p.1 p.2 ‚â§ Œµ } :=
  EMetric.mk_uniformity_basis_le (fun _ => And.left) fun Œµ Œµ‚ÇÄ =>
    let ‚ü®Œ¥, hŒ¥‚ü© := exists_between hŒµ'
    ‚ü®min Œµ Œ¥, ‚ü®lt_min Œµ‚ÇÄ hŒ¥.1, lt_of_le_of_lt (min_le_right _ _) hŒ¥.2‚ü©, min_le_left _ _‚ü©

theorem uniformity_basis_edist_nnreal :
    (ùì§ Œ±).HasBasis (fun Œµ : ‚Ñù‚â•0 => 0 < Œµ) fun Œµ => { p : Œ± √ó Œ± | edist p.1 p.2 < Œµ } :=
  EMetric.mk_uniformity_basis (fun _ => ENNReal.coe_pos.2) fun _Œµ Œµ‚ÇÄ =>
    let ‚ü®Œ¥, hŒ¥‚ü© := ENNReal.lt_iff_exists_nnreal_btwn.1 Œµ‚ÇÄ
    ‚ü®Œ¥, ENNReal.coe_pos.1 hŒ¥.1, le_of_lt hŒ¥.2‚ü©

theorem uniformity_basis_edist_nnreal_le :
    (ùì§ Œ±).HasBasis (fun Œµ : ‚Ñù‚â•0 => 0 < Œµ) fun Œµ => { p : Œ± √ó Œ± | edist p.1 p.2 ‚â§ Œµ } :=
  EMetric.mk_uniformity_basis_le (fun _ => ENNReal.coe_pos.2) fun _Œµ Œµ‚ÇÄ =>
    let ‚ü®Œ¥, hŒ¥‚ü© := ENNReal.lt_iff_exists_nnreal_btwn.1 Œµ‚ÇÄ
    ‚ü®Œ¥, ENNReal.coe_pos.1 hŒ¥.1, le_of_lt hŒ¥.2‚ü©

theorem uniformity_basis_edist_inv_nat :
    (ùì§ Œ±).HasBasis (fun _ => True) fun n : ‚Ñï => { p : Œ± √ó Œ± | edist p.1 p.2 < (‚Üën)‚Åª¬π } :=
  EMetric.mk_uniformity_basis (fun n _ ‚Ü¶ ENNReal.inv_pos.2 <| ENNReal.natCast_ne_top n) fun _Œµ Œµ‚ÇÄ ‚Ü¶
    let ‚ü®n, hn‚ü© := ENNReal.exists_inv_nat_lt (ne_of_gt Œµ‚ÇÄ)
    ‚ü®n, trivial, le_of_lt hn‚ü©

theorem uniformity_basis_edist_inv_two_pow :
    (ùì§ Œ±).HasBasis (fun _ => True) fun n : ‚Ñï => { p : Œ± √ó Œ± | edist p.1 p.2 < 2‚Åª¬π ^ n } :=
  EMetric.mk_uniformity_basis (fun _ _ ‚Ü¶ ENNReal.pow_pos (ENNReal.inv_pos.2 ENNReal.ofNat_ne_top) _)
    fun _Œµ Œµ‚ÇÄ ‚Ü¶
    let ‚ü®n, hn‚ü© := ENNReal.exists_inv_two_pow_lt (ne_of_gt Œµ‚ÇÄ)
    ‚ü®n, trivial, le_of_lt hn‚ü©

/-- Fixed size neighborhoods of the diagonal belong to the uniform structure -/
theorem edist_mem_uniformity {Œµ : ‚Ñù‚â•0‚àû} (Œµ0 : 0 < Œµ) : { p : Œ± √ó Œ± | edist p.1 p.2 < Œµ } ‚àà ùì§ Œ± :=
  mem_uniformity_edist.2 ‚ü®Œµ, Œµ0, id‚ü©

namespace EMetric

instance (priority := 900) instIsCountablyGeneratedUniformity : IsCountablyGenerated (ùì§ Œ±) :=
  isCountablyGenerated_of_seq ‚ü®_, uniformity_basis_edist_inv_nat.eq_iInf‚ü©

/-- Œµ-Œ¥ characterization of uniform continuity on a set for pseudoemetric spaces -/
theorem uniformContinuousOn_iff [PseudoEMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ±} :
    UniformContinuousOn f s ‚Üî
      ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ {a}, a ‚àà s ‚Üí ‚àÄ {b}, b ‚àà s ‚Üí edist a b < Œ¥ ‚Üí edist (f a) (f b) < Œµ :=
  uniformity_basis_edist.uniformContinuousOn_iff uniformity_basis_edist

/-- Œµ-Œ¥ characterization of uniform continuity on pseudoemetric spaces -/
theorem uniformContinuous_iff [PseudoEMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤} :
    UniformContinuous f ‚Üî ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ {a b : Œ±}, edist a b < Œ¥ ‚Üí edist (f a) (f b) < Œµ :=
  uniformity_basis_edist.uniformContinuous_iff uniformity_basis_edist

end EMetric

open EMetric

/-- Auxiliary function to replace the uniformity on a pseudoemetric space with
a uniformity which is equal to the original one, but maybe not defeq.
This is useful if one wants to construct a pseudoemetric space with a
specified uniformity. See Note [forgetful inheritance] explaining why having definitionally
the right uniformity is often important.
See note [reducible non-instances].
-/
abbrev PseudoEMetricSpace.replaceUniformity {Œ±} [U : UniformSpace Œ±] (m : PseudoEMetricSpace Œ±)
    (H : ùì§[U] = ùì§[PseudoEMetricSpace.toUniformSpace]) : PseudoEMetricSpace Œ± where
  edist := @edist _ m.toEDist
  edist_self := edist_self
  edist_comm := edist_comm
  edist_triangle := edist_triangle
  toUniformSpace := U
  uniformity_edist := H.trans (@PseudoEMetricSpace.uniformity_edist Œ± _)

/-- The extended pseudometric induced by a function taking values in a pseudoemetric space.
See note [reducible non-instances]. -/
abbrev PseudoEMetricSpace.induced {Œ± Œ≤} (f : Œ± ‚Üí Œ≤) (m : PseudoEMetricSpace Œ≤) :
    PseudoEMetricSpace Œ± where
  edist x y := edist (f x) (f y)
  edist_self _ := edist_self _
  edist_comm _ _ := edist_comm _ _
  edist_triangle _ _ _ := edist_triangle _ _ _
  toUniformSpace := UniformSpace.comap f m.toUniformSpace
  uniformity_edist := (uniformity_basis_edist.comap (Prod.map f f)).eq_biInf

/-- Pseudoemetric space instance on subsets of pseudoemetric spaces -/
instance {Œ± : Type*} {p : Œ± ‚Üí Prop} [PseudoEMetricSpace Œ±] : PseudoEMetricSpace (Subtype p) :=
  PseudoEMetricSpace.induced Subtype.val ‚Äπ_‚Ä∫

/-- The extended pseudodistance on a subset of a pseudoemetric space is the restriction of
the original pseudodistance, by definition. -/
theorem Subtype.edist_eq {p : Œ± ‚Üí Prop} (x y : Subtype p) : edist x y = edist (x : Œ±) y := rfl

/-- The extended pseudodistance on a subtype of a pseudoemetric space is the restriction of
the original pseudodistance, by definition. -/
@[simp]
theorem Subtype.edist_mk_mk {p : Œ± ‚Üí Prop} {x y : Œ±} (hx : p x) (hy : p y) :
    edist (‚ü®x, hx‚ü© : Subtype p) ‚ü®y, hy‚ü© = edist x y :=
  rfl

/-- Consider an extended distance on a topological space, for which the neighborhoods can be
expressed in terms of the distance. Then we define the emetric space structure associated to this
distance, with a topology defeq to the initial one. -/
@[reducible] noncomputable def PseudoEmetricSpace.ofEdistOfTopology {Œ± : Type*} [TopologicalSpace Œ±]
    (d : Œ± ‚Üí Œ± ‚Üí ‚Ñù‚â•0‚àû) (h_self : ‚àÄ x, d x x = 0) (h_comm : ‚àÄ x y, d x y = d y x)
    (h_triangle : ‚àÄ x y z, d x z ‚â§ d x y + d y z)
    (h_basis : ‚àÄ x, (ùìù x).HasBasis (fun c ‚Ü¶ 0 < c) (fun c ‚Ü¶ {y | d x y < c})) :
    PseudoEMetricSpace Œ± where
  edist := d
  edist_self := h_self
  edist_comm := h_comm
  edist_triangle := h_triangle
  toUniformSpace := uniformSpaceOfEDistOfHasBasis d h_self h_comm h_triangle h_basis
  uniformity_edist := rfl

namespace MulOpposite

/-- Pseudoemetric space instance on the multiplicative opposite of a pseudoemetric space. -/
@[to_additive
/-- Pseudoemetric space instance on the additive opposite of a pseudoemetric space. -/]
instance {Œ± : Type*} [PseudoEMetricSpace Œ±] : PseudoEMetricSpace Œ±·µê·µí·µñ :=
  PseudoEMetricSpace.induced unop ‚Äπ_‚Ä∫

@[to_additive]
theorem edist_unop (x y : Œ±·µê·µí·µñ) : edist (unop x) (unop y) = edist x y := rfl

@[to_additive]
theorem edist_op (x y : Œ±) : edist (op x) (op y) = edist x y := rfl

end MulOpposite

section ULift

instance : PseudoEMetricSpace (ULift Œ±) := PseudoEMetricSpace.induced ULift.down ‚Äπ_‚Ä∫

theorem ULift.edist_eq (x y : ULift Œ±) : edist x y = edist x.down y.down := rfl

@[simp]
theorem ULift.edist_up_up (x y : Œ±) : edist (ULift.up x) (ULift.up y) = edist x y := rfl

end ULift

/-- The product of two pseudoemetric spaces, with the max distance, is an extended
pseudometric spaces. We make sure that the uniform structure thus constructed is the one
corresponding to the product of uniform spaces, to avoid diamond problems. -/
instance Prod.pseudoEMetricSpaceMax [PseudoEMetricSpace Œ≤] :
    PseudoEMetricSpace (Œ± √ó Œ≤) where
  edist x y := edist x.1 y.1 ‚äî edist x.2 y.2
  edist_self x := by simp
  edist_comm x y := by simp [edist_comm]
  edist_triangle _ _ _ :=
    max_le (le_trans (edist_triangle _ _ _) (add_le_add (le_max_left _ _) (le_max_left _ _)))
      (le_trans (edist_triangle _ _ _) (add_le_add (le_max_right _ _) (le_max_right _ _)))
  uniformity_edist := uniformity_prod.trans <| by
    simp [PseudoEMetricSpace.uniformity_edist, ‚Üê iInf_inf_eq, setOf_and]
  toUniformSpace := inferInstance

theorem Prod.edist_eq [PseudoEMetricSpace Œ≤] (x y : Œ± √ó Œ≤) :
    edist x y = max (edist x.1 y.1) (edist x.2 y.2) :=
  rfl

namespace EMetric

variable {x y z : Œ±} {Œµ Œµ‚ÇÅ Œµ‚ÇÇ : ‚Ñù‚â•0‚àû} {s t : Set Œ±}

theorem mem_ball' : y ‚àà ball x Œµ ‚Üî edist x y < Œµ := by rw [edist_comm, mem_ball]

/-- `EMetric.closedBall x Œµ` is the set of all points `y` with `edist y x ‚â§ Œµ` -/
def closedBall (x : Œ±) (Œµ : ‚Ñù‚â•0‚àû) :=
  { y | edist y x ‚â§ Œµ }

@[simp] theorem mem_closedBall : y ‚àà closedBall x Œµ ‚Üî edist y x ‚â§ Œµ := Iff.rfl

theorem mem_closedBall' : y ‚àà closedBall x Œµ ‚Üî edist x y ‚â§ Œµ := by rw [edist_comm, mem_closedBall]

@[simp]
theorem closedBall_top (x : Œ±) : closedBall x ‚àû = univ :=
  eq_univ_of_forall fun _ => mem_setOf.2 le_top

theorem ball_subset_closedBall : ball x Œµ ‚äÜ closedBall x Œµ := fun _ h => le_of_lt h.out

theorem pos_of_mem_ball (hy : y ‚àà ball x Œµ) : 0 < Œµ :=
  lt_of_le_of_lt (zero_le _) hy

theorem mem_ball_self (h : 0 < Œµ) : x ‚àà ball x Œµ := by
  rwa [mem_ball, edist_self]

theorem mem_closedBall_self : x ‚àà closedBall x Œµ := by
  rw [mem_closedBall, edist_self]; apply zero_le

theorem mem_ball_comm : x ‚àà ball y Œµ ‚Üî y ‚àà ball x Œµ := by rw [mem_ball', mem_ball]

theorem mem_closedBall_comm : x ‚àà closedBall y Œµ ‚Üî y ‚àà closedBall x Œµ := by
  rw [mem_closedBall', mem_closedBall]

@[gcongr]
theorem ball_subset_ball (h : Œµ‚ÇÅ ‚â§ Œµ‚ÇÇ) : ball x Œµ‚ÇÅ ‚äÜ ball x Œµ‚ÇÇ := fun _y (yx : _ < Œµ‚ÇÅ) =>
  lt_of_lt_of_le yx h

@[gcongr]
theorem closedBall_subset_closedBall (h : Œµ‚ÇÅ ‚â§ Œµ‚ÇÇ) : closedBall x Œµ‚ÇÅ ‚äÜ closedBall x Œµ‚ÇÇ :=
  fun _y (yx : _ ‚â§ Œµ‚ÇÅ) => le_trans yx h

theorem ball_disjoint (h : Œµ‚ÇÅ + Œµ‚ÇÇ ‚â§ edist x y) : Disjoint (ball x Œµ‚ÇÅ) (ball y Œµ‚ÇÇ) :=
  Set.disjoint_left.mpr fun z h‚ÇÅ h‚ÇÇ =>
    (edist_triangle_left x y z).not_gt <| (ENNReal.add_lt_add h‚ÇÅ h‚ÇÇ).trans_le h

theorem ball_subset (h : edist x y + Œµ‚ÇÅ ‚â§ Œµ‚ÇÇ) (h' : edist x y ‚â† ‚àû) : ball x Œµ‚ÇÅ ‚äÜ ball y Œµ‚ÇÇ :=
  fun z zx =>
  calc
    edist z y ‚â§ edist z x + edist x y := edist_triangle _ _ _
    _ = edist x y + edist z x := add_comm _ _
    _ < edist x y + Œµ‚ÇÅ := ENNReal.add_lt_add_left h' zx
    _ ‚â§ Œµ‚ÇÇ := h

theorem exists_ball_subset_ball (h : y ‚àà ball x Œµ) : ‚àÉ Œµ' > 0, ball y Œµ' ‚äÜ ball x Œµ := by
  have : 0 < Œµ - edist y x := by simpa using h
  refine ‚ü®Œµ - edist y x, this, ball_subset ?_ (ne_top_of_lt h)‚ü©
  exact (add_tsub_cancel_of_le (mem_ball.mp h).le).le

theorem ball_eq_empty_iff : ball x Œµ = ‚àÖ ‚Üî Œµ = 0 :=
  eq_empty_iff_forall_notMem.trans
    ‚ü®fun h => le_bot_iff.1 (le_of_not_gt fun Œµ0 => h _ (mem_ball_self Œµ0)), fun Œµ0 _ h =>
      not_lt_of_ge (le_of_eq Œµ0) (pos_of_mem_ball h)‚ü©

theorem ordConnected_setOf_closedBall_subset (x : Œ±) (s : Set Œ±) :
    OrdConnected { r | closedBall x r ‚äÜ s } :=
  ‚ü®fun _ _ _ h‚ÇÅ _ h‚ÇÇ => (closedBall_subset_closedBall h‚ÇÇ.2).trans h‚ÇÅ‚ü©

theorem ordConnected_setOf_ball_subset (x : Œ±) (s : Set Œ±) : OrdConnected { r | ball x r ‚äÜ s } :=
  ‚ü®fun _ _ _ h‚ÇÅ _ h‚ÇÇ => (ball_subset_ball h‚ÇÇ.2).trans h‚ÇÅ‚ü©

/-- Relation ‚Äútwo points are at a finite edistance‚Äù is an equivalence relation. -/
def edistLtTopSetoid : Setoid Œ± where
  r x y := edist x y < ‚ä§
  iseqv :=
    ‚ü®fun x => by rw [edist_self]; exact ENNReal.coe_lt_top,
      fun h => by rwa [edist_comm], fun hxy hyz =>
        lt_of_le_of_lt (edist_triangle _ _ _) (ENNReal.add_lt_top.2 ‚ü®hxy, hyz‚ü©)‚ü©

@[simp]
theorem ball_zero : ball x 0 = ‚àÖ := by rw [EMetric.ball_eq_empty_iff]

theorem nhds_basis_eball : (ùìù x).HasBasis (fun Œµ : ‚Ñù‚â•0‚àû => 0 < Œµ) (ball x) :=
  nhds_basis_uniformity uniformity_basis_edist

theorem nhdsWithin_basis_eball : (ùìù[s] x).HasBasis (fun Œµ : ‚Ñù‚â•0‚àû => 0 < Œµ) fun Œµ => ball x Œµ ‚à© s :=
  nhdsWithin_hasBasis nhds_basis_eball s

theorem nhds_basis_closed_eball : (ùìù x).HasBasis (fun Œµ : ‚Ñù‚â•0‚àû => 0 < Œµ) (closedBall x) :=
  nhds_basis_uniformity uniformity_basis_edist_le

theorem nhdsWithin_basis_closed_eball :
    (ùìù[s] x).HasBasis (fun Œµ : ‚Ñù‚â•0‚àû => 0 < Œµ) fun Œµ => closedBall x Œµ ‚à© s :=
  nhdsWithin_hasBasis nhds_basis_closed_eball s

theorem nhds_eq : ùìù x = ‚®Ö Œµ > 0, ùìü (ball x Œµ) :=
  nhds_basis_eball.eq_biInf

theorem mem_nhds_iff : s ‚àà ùìù x ‚Üî ‚àÉ Œµ > 0, ball x Œµ ‚äÜ s :=
  nhds_basis_eball.mem_iff

theorem mem_nhdsWithin_iff : s ‚àà ùìù[t] x ‚Üî ‚àÉ Œµ > 0, ball x Œµ ‚à© t ‚äÜ s :=
  nhdsWithin_basis_eball.mem_iff

section

variable [PseudoEMetricSpace Œ≤] {f : Œ± ‚Üí Œ≤}

theorem tendsto_nhdsWithin_nhdsWithin {t : Set Œ≤} {a b} :
    Tendsto f (ùìù[s] a) (ùìù[t] b) ‚Üî
      ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí edist x a < Œ¥ ‚Üí f x ‚àà t ‚àß edist (f x) b < Œµ :=
  (nhdsWithin_basis_eball.tendsto_iff nhdsWithin_basis_eball).trans <|
    forall‚ÇÇ_congr fun Œµ _ => exists_congr fun Œ¥ => and_congr_right fun _ =>
      forall_congr' fun x => by simp; tauto

theorem tendsto_nhdsWithin_nhds {a b} :
    Tendsto f (ùìù[s] a) (ùìù b) ‚Üî
      ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ {x : Œ±}, x ‚àà s ‚Üí edist x a < Œ¥ ‚Üí edist (f x) b < Œµ := by
  rw [‚Üê nhdsWithin_univ b, tendsto_nhdsWithin_nhdsWithin]
  simp only [mem_univ, true_and]

theorem tendsto_nhds_nhds {a b} :
    Tendsto f (ùìù a) (ùìù b) ‚Üî ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ ‚¶Éx‚¶Ñ, edist x a < Œ¥ ‚Üí edist (f x) b < Œµ :=
  nhds_basis_eball.tendsto_iff nhds_basis_eball

end

theorem isOpen_iff : IsOpen s ‚Üî ‚àÄ x ‚àà s, ‚àÉ Œµ > 0, ball x Œµ ‚äÜ s := by
  simp [isOpen_iff_nhds, mem_nhds_iff]

@[simp] theorem isOpen_ball : IsOpen (ball x Œµ) :=
  isOpen_iff.2 fun _ => exists_ball_subset_ball

theorem isClosed_ball_top : IsClosed (ball x ‚ä§) :=
  isOpen_compl_iff.1 <| isOpen_iff.2 fun _y hy =>
    ‚ü®‚ä§, ENNReal.coe_lt_top, fun _z hzy hzx =>
      hy (edistLtTopSetoid.trans (edistLtTopSetoid.symm hzy) hzx)‚ü©

theorem ball_mem_nhds (x : Œ±) {Œµ : ‚Ñù‚â•0‚àû} (Œµ0 : 0 < Œµ) : ball x Œµ ‚àà ùìù x :=
  isOpen_ball.mem_nhds (mem_ball_self Œµ0)

theorem closedBall_mem_nhds (x : Œ±) {Œµ : ‚Ñù‚â•0‚àû} (Œµ0 : 0 < Œµ) : closedBall x Œµ ‚àà ùìù x :=
  mem_of_superset (ball_mem_nhds x Œµ0) ball_subset_closedBall

theorem ball_prod_same [PseudoEMetricSpace Œ≤] (x : Œ±) (y : Œ≤) (r : ‚Ñù‚â•0‚àû) :
    ball x r √óÀ¢ ball y r = ball (x, y) r :=
  ext fun z => by simp [Prod.edist_eq]

theorem closedBall_prod_same [PseudoEMetricSpace Œ≤] (x : Œ±) (y : Œ≤) (r : ‚Ñù‚â•0‚àû) :
    closedBall x r √óÀ¢ closedBall y r = closedBall (x, y) r :=
  ext fun z => by simp [Prod.edist_eq]

/-- Œµ-characterization of the closure in pseudoemetric spaces -/
theorem mem_closure_iff : x ‚àà closure s ‚Üî ‚àÄ Œµ > 0, ‚àÉ y ‚àà s, edist x y < Œµ :=
  (mem_closure_iff_nhds_basis nhds_basis_eball).trans <| by simp only [mem_ball, edist_comm x]

theorem tendsto_nhds {f : Filter Œ≤} {u : Œ≤ ‚Üí Œ±} {a : Œ±} :
    Tendsto u f (ùìù a) ‚Üî ‚àÄ Œµ > 0, ‚àÄ·∂† x in f, edist (u x) a < Œµ :=
  nhds_basis_eball.tendsto_right_iff

theorem tendsto_atTop [Nonempty Œ≤] [SemilatticeSup Œ≤] {u : Œ≤ ‚Üí Œ±} {a : Œ±} :
    Tendsto u atTop (ùìù a) ‚Üî ‚àÄ Œµ > 0, ‚àÉ N, ‚àÄ n ‚â• N, edist (u n) a < Œµ :=
  (atTop_basis.tendsto_iff nhds_basis_eball).trans <| by
    simp only [true_and, mem_Ici, mem_ball]

section

end

section Compact

-- TODO: generalize to a uniform space with metrizable uniformity
/-- For a set `s` in a pseudo emetric space, if for every `Œµ > 0` there exists a countable
set that is `Œµ`-dense in `s`, then there exists a countable subset `t ‚äÜ s` that is dense in `s`. -/
theorem subset_countable_closure_of_almost_dense_set (s : Set Œ±)
    (hs : ‚àÄ Œµ > 0, ‚àÉ t : Set Œ±, t.Countable ‚àß s ‚äÜ ‚ãÉ x ‚àà t, closedBall x Œµ) :
    ‚àÉ t, t ‚äÜ s ‚àß t.Countable ‚àß s ‚äÜ closure t := by
  rcases s.eq_empty_or_nonempty with (rfl | ‚ü®x‚ÇÄ, hx‚ÇÄ‚ü©)
  ¬∑ exact ‚ü®‚àÖ, empty_subset _, countable_empty, empty_subset _‚ü©
  choose! T hTc hsT using fun n : ‚Ñï => hs n‚Åª¬π (by simp)
  have : ‚àÄ r x, ‚àÉ y ‚àà s, closedBall x r ‚à© s ‚äÜ closedBall y (r * 2) := fun r x => by
    rcases (closedBall x r ‚à© s).eq_empty_or_nonempty with (he | ‚ü®y, hxy, hys‚ü©)
    ¬∑ refine ‚ü®x‚ÇÄ, hx‚ÇÄ, ?_‚ü©
      rw [he]
      exact empty_subset _
    ¬∑ refine ‚ü®y, hys, fun z hz => ?_‚ü©
      calc
        edist z y ‚â§ edist z x + edist y x := edist_triangle_right _ _ _
        _ ‚â§ r + r := add_le_add hz.1 hxy
        _ = r * 2 := (mul_two r).symm
  choose f hfs hf using this
  refine
    ‚ü®‚ãÉ n : ‚Ñï, f n‚Åª¬π '' T n, iUnion_subset fun n => image_subset_iff.2 fun z _ => hfs _ _,
      countable_iUnion fun n => (hTc n).image _, ?_‚ü©
  refine fun x hx => mem_closure_iff.2 fun Œµ Œµ0 => ?_
  rcases ENNReal.exists_inv_nat_lt (ENNReal.half_pos Œµ0.lt.ne').ne' with ‚ü®n, hn‚ü©
  rcases mem_iUnion‚ÇÇ.1 (hsT n hx) with ‚ü®y, hyn, hyx‚ü©
  refine ‚ü®f n‚Åª¬π y, mem_iUnion.2 ‚ü®n, mem_image_of_mem _ hyn‚ü©, ?_‚ü©
  calc
    edist x (f n‚Åª¬π y) ‚â§ (n : ‚Ñù‚â•0‚àû)‚Åª¬π * 2 := hf _ _ ‚ü®hyx, hx‚ü©
    _ < Œµ := ENNReal.mul_lt_of_lt_div hn

end Compact

end EMetric

namespace Subtype

@[simp]
theorem preimage_emetricBall {p : Œ± ‚Üí Prop} (a : {a // p a}) (r : ‚Ñù‚â•0‚àû) :
    Subtype.val ‚Åª¬π' (ball a.1 r) = ball a r :=
  rfl

@[simp]
theorem preimage_emetricClosedBall {p : Œ± ‚Üí Prop} (a : {a // p a}) (r : ‚Ñù‚â•0‚àû) :
    Subtype.val ‚Åª¬π' (closedBall a.1 r) = closedBall a r :=
  rfl

@[simp]
theorem image_emetricBall {p : Œ± ‚Üí Prop} (a : {a // p a}) (r : ‚Ñù‚â•0‚àû) :
    Subtype.val '' (ball a r) = ball a.1 r ‚à© {a | p a} := by
  rw [‚Üê preimage_emetricBall, image_preimage_eq_inter_range, range_val_subtype]

@[simp]
theorem image_emetricClosedBall {p : Œ± ‚Üí Prop} (a : {a // p a}) (r : ‚Ñù‚â•0‚àû) :
    Subtype.val '' (closedBall a r) = closedBall a.1 r ‚à© {a | p a} := by
  rw [‚Üê preimage_emetricClosedBall, image_preimage_eq_inter_range, range_val_subtype]

end Subtype

/-- An extended metric space is a type endowed with a `‚Ñù‚â•0‚àû`-valued distance `edist` satisfying
`edist x y = 0 ‚Üî x = y`, commutativity `edist x y = edist y x`, and the triangle inequality
`edist x z ‚â§ edist x y + edist y z`.

See pseudo extended metric spaces (`PseudoEMetricSpace`) for the similar class with the
`edist x y = 0 ‚Üî x = y` assumption weakened to `edist x x = 0`.

Any extended metric space is a T1 topological space and a uniform space (see `TopologicalSpace`,
`T1Space`, `UniformSpace`), where the topology and uniformity come from the metric.

We make the uniformity/topology part of the data instead of deriving it from the metric.
This e.g. ensures that we do not get a diamond when doing
`[EMetricSpace Œ±] [EMetricSpace Œ≤] : TopologicalSpace (Œ± √ó Œ≤)`:
The product metric and product topology agree, but not definitionally so.
See Note [forgetful inheritance]. -/
class EMetricSpace (Œ± : Type u) : Type u extends PseudoEMetricSpace Œ± where
  eq_of_edist_eq_zero : ‚àÄ {x y : Œ±}, edist x y = 0 ‚Üí x = y

@[ext]
protected theorem EMetricSpace.ext
    {Œ± : Type*} {m m' : EMetricSpace Œ±} (h : m.toEDist = m'.toEDist) : m = m' := by
  cases m
  cases m'
  congr
  ext1
  assumption

variable {Œ≥ : Type w} [EMetricSpace Œ≥]

export EMetricSpace (eq_of_edist_eq_zero)

/-- Characterize the equality of points by the vanishing of their extended distance -/
@[simp]
theorem edist_eq_zero {x y : Œ≥} : edist x y = 0 ‚Üî x = y :=
  ‚ü®eq_of_edist_eq_zero, fun h => h ‚ñ∏ edist_self _‚ü©

@[simp]
theorem zero_eq_edist {x y : Œ≥} : 0 = edist x y ‚Üî x = y := eq_comm.trans edist_eq_zero

theorem edist_le_zero {x y : Œ≥} : edist x y ‚â§ 0 ‚Üî x = y :=
  nonpos_iff_eq_zero.trans edist_eq_zero

@[simp]
theorem edist_pos {x y : Œ≥} : 0 < edist x y ‚Üî x ‚â† y := by simp [‚Üê not_le]

@[simp] lemma EMetric.closedBall_zero (x : Œ≥) : closedBall x 0 = {x} := by ext; simp

/-- Two points coincide if their distance is `< Œµ` for all positive Œµ -/
theorem eq_of_forall_edist_le {x y : Œ≥} (h : ‚àÄ Œµ > 0, edist x y ‚â§ Œµ) : x = y :=
  eq_of_edist_eq_zero (eq_of_le_of_forall_lt_imp_le_of_dense bot_le h)

/-- Auxiliary function to replace the uniformity on an emetric space with
a uniformity which is equal to the original one, but maybe not defeq.
This is useful if one wants to construct an emetric space with a
specified uniformity. See Note [forgetful inheritance] explaining why having definitionally
the right uniformity is often important.
See note [reducible non-instances].
-/
abbrev EMetricSpace.replaceUniformity {Œ≥} [U : UniformSpace Œ≥] (m : EMetricSpace Œ≥)
    (H : ùì§[U] = ùì§[PseudoEMetricSpace.toUniformSpace]) : EMetricSpace Œ≥ where
  edist := @edist _ m.toEDist
  edist_self := edist_self
  eq_of_edist_eq_zero := @eq_of_edist_eq_zero _ _
  edist_comm := edist_comm
  edist_triangle := edist_triangle
  toUniformSpace := U
  uniformity_edist := H.trans (@PseudoEMetricSpace.uniformity_edist Œ≥ _)

/-- Auxiliary function to replace the topology on an emetric space with
a topology which is equal to the original one, but maybe not defeq.
This is useful if one wants to construct an emetric space with a
specified topology. See Note [forgetful inheritance] explaining why having definitionally
the right topology is often important.
See note [reducible non-instances].
-/
abbrev EMetricSpace.replaceTopology {Œ≥} [T : TopologicalSpace Œ≥] (m : EMetricSpace Œ≥)
    (H : T = m.toUniformSpace.toTopologicalSpace) : EMetricSpace Œ≥ where
  edist := @edist _ m.toEDist
  edist_self := edist_self
  eq_of_edist_eq_zero := @eq_of_edist_eq_zero _ _
  edist_comm := edist_comm
  edist_triangle := edist_triangle
  toUniformSpace := m.toUniformSpace.replaceTopology H
  uniformity_edist := PseudoEMetricSpace.uniformity_edist

/-- The extended metric induced by an injective function taking values in an emetric space.
See Note [reducible non-instances]. -/
abbrev EMetricSpace.induced {Œ≥ Œ≤} (f : Œ≥ ‚Üí Œ≤) (hf : Function.Injective f) (m : EMetricSpace Œ≤) :
    EMetricSpace Œ≥ :=
  { PseudoEMetricSpace.induced f m.toPseudoEMetricSpace with
    eq_of_edist_eq_zero := fun h => hf (edist_eq_zero.1 h) }

/-- EMetric space instance on subsets of emetric spaces -/
instance {Œ± : Type*} {p : Œ± ‚Üí Prop} [EMetricSpace Œ±] : EMetricSpace (Subtype p) :=
  EMetricSpace.induced Subtype.val Subtype.coe_injective ‚Äπ_‚Ä∫

/-- EMetric space instance on the multiplicative opposite of an emetric space. -/
@[to_additive /-- EMetric space instance on the additive opposite of an emetric space. -/]
instance {Œ± : Type*} [EMetricSpace Œ±] : EMetricSpace Œ±·µê·µí·µñ :=
  EMetricSpace.induced MulOpposite.unop MulOpposite.unop_injective ‚Äπ_‚Ä∫

instance {Œ± : Type*} [EMetricSpace Œ±] : EMetricSpace (ULift Œ±) :=
  EMetricSpace.induced ULift.down ULift.down_injective ‚Äπ_‚Ä∫

/-- Reformulation of the uniform structure in terms of the extended distance -/
theorem uniformity_edist : ùì§ Œ≥ = ‚®Ö Œµ > 0, ùìü { p : Œ≥ √ó Œ≥ | edist p.1 p.2 < Œµ } :=
  PseudoEMetricSpace.uniformity_edist

/-!
### `Additive`, `Multiplicative`

The distance on those type synonyms is inherited without change.
-/


open Additive Multiplicative

section

variable [EDist X]

instance : EDist (Additive X) := ‚ÄπEDist X‚Ä∫
instance : EDist (Multiplicative X) := ‚ÄπEDist X‚Ä∫

@[simp]
theorem edist_ofMul (a b : X) : edist (ofMul a) (ofMul b) = edist a b :=
  rfl

@[simp]
theorem edist_ofAdd (a b : X) : edist (ofAdd a) (ofAdd b) = edist a b :=
  rfl

@[simp]
theorem edist_toMul (a b : Additive X) : edist a.toMul b.toMul = edist a b :=
  rfl

@[simp]
theorem edist_toAdd (a b : Multiplicative X) : edist a.toAdd b.toAdd = edist a b :=
  rfl

end

instance [PseudoEMetricSpace X] : PseudoEMetricSpace (Additive X) := ‚ÄπPseudoEMetricSpace X‚Ä∫
instance [PseudoEMetricSpace X] : PseudoEMetricSpace (Multiplicative X) := ‚ÄπPseudoEMetricSpace X‚Ä∫
instance [EMetricSpace X] : EMetricSpace (Additive X) := ‚ÄπEMetricSpace X‚Ä∫
instance [EMetricSpace X] : EMetricSpace (Multiplicative X) := ‚ÄπEMetricSpace X‚Ä∫

/-!
### Order dual

The distance on this type synonym is inherited without change.
-/


open OrderDual

section

variable [EDist X]

instance : EDist X·µí·µà := ‚ÄπEDist X‚Ä∫

@[simp]
theorem edist_toDual (a b : X) : edist (toDual a) (toDual b) = edist a b :=
  rfl

@[simp]
theorem edist_ofDual (a b : X·µí·µà) : edist (ofDual a) (ofDual b) = edist a b :=
  rfl

end

section

/-- A `WeakPseudoEMetricSpace` is a topological space endowed with a `‚Ñù‚â•0‚àû`-value distance `edist`
which is *almost* an extended pseudometric space: the `edist` is reflexive, commutative and
satisfies the triangle inequality, but the topology on `Œ±` need not *equal* the topology induced
by the `edist`. (It must be at least as fine, and agree with it on balls of finite radius.)

This generalises both pseudo extended metric spaces and `‚Ñù‚â•0‚àû` (which have an extended distance,
which does not induce the order topology there). -/
class WeakPseudoEMetricSpace
    (Œ± : Type u) [œÑ : TopologicalSpace Œ±] : Type u extends EDist Œ±  where
  edist_self : ‚àÄ x : Œ±, edist x x = 0
  edist_comm : ‚àÄ x y : Œ±, edist x y = edist y x
  edist_triangle : ‚àÄ x y z : Œ±, edist x z ‚â§ edist x y + edist y z
  /-- The topology on `Œ±` is at most as fine as the topology generated by the `edist`: -/
  topology_le :
    (uniformSpaceOfEDist edist edist_self edist_comm edist_triangle).toTopologicalSpace ‚â§ œÑ
  /-- The ambient topology on `Œ±` matches the `edist` topology on balls of finite radius`. -/
  topology_eq_on_restrict :
    ‚àÄ (x : Œ±) (r : ‚Ñù‚â•0‚àû),
    IsOpen[instTopologicalSpaceSubtype (t := œÑ)] ((EMetric.ball x ‚ä§) ‚Üì‚à© (ball x r))

@[ext]
protected theorem WeakPseudoEMetricSpace.ext
    {Œ± : Type*} [TopologicalSpace Œ±] {m m' : WeakPseudoEMetricSpace Œ±}
      (h : m.toEDist = m'.toEDist) : m = m' := by
  obtain ‚ü®_, _, _, _, _‚ü© := m
  obtain ‚ü®_, _, _, _, _‚ü© := m'
  congr 1

/-- Every PseudoEMetricSpace has a WeakPseudoEMetricSpace structure by
  using the topology induced by edist. -/
@[reducible]
def PseudoEMetricSpace.toWeakPseudoEMetricSpace (Œ± : Type u) [inst : PseudoEMetricSpace Œ±] :
    WeakPseudoEMetricSpace Œ± where
  edist_self := edist_self
  edist_comm := edist_comm
  edist_triangle := edist_triangle
  topology_le := by rw [uniformSpace_edist]
  topology_eq_on_restrict := by
    intro x r
    refine IsOpen.preimage_val ?_
    exact isOpen_ball

/-- A WeakPseudoEMetricSpace has a PseudoEMetricSpace structure by forgetting the topology. -/
@[reducible]
def WeakPseudoEMetricSpace.toPseudoEMetricSpace
    (Œ± : Type u) [TopologicalSpace Œ±] [inst : WeakPseudoEMetricSpace Œ±] :
      PseudoEMetricSpace Œ± where
  edist := inst.edist
  edist_self := edist_self
  edist_comm := edist_comm
  edist_triangle := edist_triangle
  toUniformSpace := uniformSpaceOfEDist edist edist_self edist_comm edist_triangle
  uniformity_edist := rfl

theorem toPseudoEMetricSpaceToUniformSpace_uniformSpaceOfEDist
    (Œ± : Type u) [TopologicalSpace Œ±] {m : WeakPseudoEMetricSpace Œ±} :
    ((WeakPseudoEMetricSpace.toPseudoEMetricSpace Œ±).toUniformSpace).toTopologicalSpace =
    ((uniformSpaceOfEDist
    m.edist m.edist_self m.edist_comm m.edist_triangle)).toTopologicalSpace := rfl

theorem toPseudoEMetricSpaceToUniformSpace_uniformSpaceOfEDist_congr
    {Œ± : Type u} [TopologicalSpace Œ±] {m : WeakPseudoEMetricSpace Œ±} (s : Set Œ±) :
    IsOpen[((WeakPseudoEMetricSpace.toPseudoEMetricSpace Œ±).toUniformSpace).toTopologicalSpace] s
    ‚Üî IsOpen[((uniformSpaceOfEDist
    m.edist m.edist_self m.edist_comm m.edist_triangle)).toTopologicalSpace] s := by rfl


/-- WeakPseudoEMetricSpace can be induced -/
abbrev WeakPseudoEMetricSpace.induced
  {Œ± Œ≤ : Type*} [n : TopologicalSpace Œ≤] (f : Œ± ‚Üí Œ≤) (m : WeakPseudoEMetricSpace Œ≤) :
    @WeakPseudoEMetricSpace Œ± (TopologicalSpace.induced f n) := by
  letI := TopologicalSpace.induced f n
  refine {
    edist := fun x y ‚Ü¶ edist (f x) (f y)
    edist_self x := edist_self (f x)
    edist_comm x y := edist_comm (f x) (f y)
    edist_triangle x y z := edist_triangle (f x) (f y) (f z)
    topology_le := by
      intro s hs
      obtain ‚ü®t, th, ts‚ü© := isOpen_induced_iff.1 hs
      clear hs
      rw [‚Üê ts]
      replace th := m.topology_le t th
      letI : TopologicalSpace Œ± := TopologicalSpace.induced f n
      rw [‚Üê toPseudoEMetricSpaceToUniformSpace_uniformSpaceOfEDist_congr'] at th ‚ä¢
      letI := pseudoEMetricSpaceOfEDist' (fun x y ‚Ü¶ edist (f x) (f y)) (fun _ ‚Ü¶ m.edist_self (f _))
        (fun x y ‚Ü¶ m.edist_comm (f x) (f y)) (fun x y z ‚Ü¶ m.edist_triangle (f x) (f y) (f z))
      letI := m.toPseudoEMetricSpace
      rw [isOpen_iff] at th ‚ä¢
      intro x xh
      obtain ‚ü®Œµ, Œµp, Œµh‚ü© := th (f x) xh
      use Œµ
      refine ‚ü®Œµp, ?_‚ü©
      have ss : ball x Œµ ‚äÜ f ‚Åª¬π' (ball (f x) Œµ) := by
        intro
        simp only [mem_ball, mem_preimage]
        exact id
      exact subset_trans ss (preimage_mono Œµh)
    topology_eq_on_restrict := by
      intro x r
      obtain ‚ü®u, hu, uy‚ü© := m.6 (f x) r
      use (f ‚Åª¬π' u)
      refine ‚ü®isOpen_induced hu, ?_‚ü©
      ext ‚ü®z, zh‚ü©
      simp only [mem_preimage, mem_ball]
      have (i : { x_1 // x_1 ‚àà ball (f x) ‚ä§ }) :
          i ‚àà Subtype.val ‚Åª¬π' u ‚Üî i ‚àà ball (f x) ‚ä§ ‚Üì‚à© ball (f x) r :=
        Iff.of_eq (congrFun uy i)
      simp only [mem_preimage, mem_ball, Subtype.forall] at this
      exact this (f z) zh
  }

/-- Weak Pseudoemetric space instance on subsets of weak pseudoemetric spaces -/
instance {Œ± : Type*} {p : Œ± ‚Üí Prop} [TopologicalSpace Œ±] [WeakPseudoEMetricSpace Œ±] :
    WeakPseudoEMetricSpace (Subtype p) :=
  WeakPseudoEMetricSpace.induced Subtype.val ‚Äπ_‚Ä∫

/-- A weak extended metric space extends a `WeakPseudoEMetricSpace` with the condition
`edist x y = 0 ‚Üî x = y`. -/
class WeakEMetricSpace
    (Œ± : Type u) [TopologicalSpace Œ±] : Type u extends WeakPseudoEMetricSpace Œ± where
  eq_of_edist_eq_zero : ‚àÄ {x y : Œ±}, edist x y = 0 ‚Üí x = y

@[ext]
protected theorem WeakEMetricSpace.ext {Œ± : Type*} [TopologicalSpace Œ±] {m m' : WeakEMetricSpace Œ±}
    (h : m.toEDist = m'.toEDist) : m = m' := by
  cases m
  cases m'
  congr
  ext1
  assumption

/-- Every EMetricSpace has a WeakEMetricSpace structure by
  using the topology induced by edist. -/
@[reducible]
def EMetricSpace.toWeakEMetricSpace (Œ± : Type u) [inst : EMetricSpace Œ±] :
    WeakEMetricSpace Œ± where
  edist x y := edist x y
  edist_self := edist_self
  edist_comm := edist_comm
  edist_triangle := edist_triangle
  topology_le := inst.toWeakPseudoEMetricSpace.5
  topology_eq_on_restrict := inst.toWeakPseudoEMetricSpace.6
  eq_of_edist_eq_zero := eq_of_edist_eq_zero

/-- Every WeakEMetricSpace is a EMetricSpace by forgetting the topology. -/
@[reducible]
def WeakEMetricSpace.toEMetricSpace (Œ± : Type u) [TopologicalSpace Œ±] [inst : WeakEMetricSpace Œ±] :
    EMetricSpace Œ± where
  edist := edist
  edist_self := inst.edist_self
  edist_comm := inst.edist_comm
  edist_triangle := inst.edist_triangle
  uniformity_edist := rfl
  eq_of_edist_eq_zero := inst.eq_of_edist_eq_zero

/-- WeakEMetric space can be induced with injective -/
abbrev WeakEMetricSpace.induced
  {Œ± Œ≤ : Type*} [n : TopologicalSpace Œ≤]
  {f : Œ± ‚Üí Œ≤} (hf : Function.Injective f) (m : WeakEMetricSpace Œ≤) :
    @WeakEMetricSpace Œ± (TopologicalSpace.induced f n) :=
  letI := TopologicalSpace.induced f n
  { WeakPseudoEMetricSpace.induced f m.toWeakPseudoEMetricSpace with
    eq_of_edist_eq_zero := fun h => hf (m.eq_of_edist_eq_zero h) }

/-- Weak EMetric space instance on subsets of emetric spaces -/
instance {Œ± : Type*} {p : Œ± ‚Üí Prop} [TopologicalSpace Œ±] [WeakEMetricSpace Œ±] :
    WeakEMetricSpace (Subtype p) :=
  WeakEMetricSpace.induced Subtype.coe_injective ‚Äπ_‚Ä∫

@[simp] theorem weakedist_self
    {Œ± : Type*} [TopologicalSpace Œ±] [m : WeakPseudoEMetricSpace Œ±] (a : Œ±) :
    edist a a = 0 := by rw [m.edist_self]

end

instance [TopologicalSpace X] [WeakPseudoEMetricSpace X] : WeakPseudoEMetricSpace X·µí·µà :=
  ‚ÄπWeakPseudoEMetricSpace X‚Ä∫
instance [TopologicalSpace X] [WeakEMetricSpace X] : WeakEMetricSpace X·µí·µà :=
  ‚ÄπWeakEMetricSpace X‚Ä∫
instance [PseudoEMetricSpace X] : PseudoEMetricSpace X·µí·µà :=
  ‚ÄπPseudoEMetricSpace X‚Ä∫
instance [EMetricSpace X] : EMetricSpace X·µí·µà :=
  ‚ÄπEMetricSpace X‚Ä∫
