/-
Copyright (c) 2022 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Order.Interval.Set.ProjIcc
import Mathlib.Tactic.Finiteness
import Mathlib.Topology.Semicontinuous
import Mathlib.Topology.UniformSpace.UniformConvergenceTopology

/-!
# Functions of bounded variation

We study functions of bounded variation. In particular, we show that a bounded variation function
is a difference of monotone functions, and differentiable almost everywhere. This implies that
Lipschitz functions from the real line into finite-dimensional vector space are also differentiable
almost everywhere.

## Main definitions and results

* `eVariationOn f s` is the total variation of the function `f` on the set `s`, in `â„â‰¥0âˆ`.
* `BoundedVariationOn f s` registers that the variation of `f` on `s` is finite.
* `LocallyBoundedVariationOn f s` registers that `f` has finite variation on any compact
  subinterval of `s`.
* `variationOnFromTo f s a b` is the signed variation of `f` on `s âˆ© Icc a b`, converted to `â„`.

* `eVariationOn.Icc_add_Icc` states that the variation of `f` on `[a, c]` is the sum of its
  variations on `[a, b]` and `[b, c]`.
* `LocallyBoundedVariationOn.exists_monotoneOn_sub_monotoneOn` proves that a function
  with locally bounded variation is the difference of two monotone functions.
* `LipschitzWith.locallyBoundedVariationOn` shows that a Lipschitz function has locally
  bounded variation.

We also give several variations around these results.

## Implementation

We define the variation as an extended nonnegative real, to allow for infinite variation. This makes
it possible to use the complete linear order structure of `â„â‰¥0âˆ`. The proofs would be much
more tedious with an `â„`-valued or `â„â‰¥0`-valued variation, since one would always need to check
that the sets one uses are nonempty and bounded above as these are only conditionally complete.
-/


open scoped NNReal ENNReal Topology UniformConvergence
open Set Filter

variable {Î± : Type*} [LinearOrder Î±] {E : Type*} [PseudoEMetricSpace E]

/-- The (extended-real-valued) variation of a function `f` on a set `s` inside a linear order is
the supremum of the sum of `edist (f (u (i+1))) (f (u i))` over all finite increasing
sequences `u` in `s`. -/
noncomputable def eVariationOn (f : Î± â†’ E) (s : Set Î±) : â„â‰¥0âˆ :=
  â¨† p : â„• Ã— { u : â„• â†’ Î± // Monotone u âˆ§ âˆ€ i, u i âˆˆ s },
    âˆ‘ i âˆˆ Finset.range p.1, edist (f (p.2.1 (i + 1))) (f (p.2.1 i))

/-- A function has bounded variation on a set `s` if its total variation there is finite. -/
def BoundedVariationOn (f : Î± â†’ E) (s : Set Î±) :=
  eVariationOn f s â‰  âˆ

/-- A function has locally bounded variation on a set `s` if, given any interval `[a, b]` with
endpoints in `s`, then the function has finite variation on `s âˆ© [a, b]`. -/
def LocallyBoundedVariationOn (f : Î± â†’ E) (s : Set Î±) :=
  âˆ€ a b, a âˆˆ s â†’ b âˆˆ s â†’ BoundedVariationOn f (s âˆ© Icc a b)

/-! ## Basic computations of variation -/

namespace eVariationOn

theorem nonempty_monotone_mem {s : Set Î±} (hs : s.Nonempty) :
    Nonempty { u // Monotone u âˆ§ âˆ€ i : â„•, u i âˆˆ s } := by
  obtain âŸ¨x, hxâŸ© := hs
  exact âŸ¨âŸ¨fun _ => x, fun i j _ => le_rfl, fun _ => hxâŸ©âŸ©

theorem eq_of_edist_zero_on {f f' : Î± â†’ E} {s : Set Î±} (h : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ edist (f x) (f' x) = 0) :
    eVariationOn f s = eVariationOn f' s := by
  dsimp only [eVariationOn]
  congr 1 with p : 1
  congr 1 with i : 1
  rw [edist_congr_right (h <| p.snd.prop.2 (i + 1)), edist_congr_left (h <| p.snd.prop.2 i)]

theorem eq_of_eqOn {f f' : Î± â†’ E} {s : Set Î±} (h : EqOn f f' s) :
    eVariationOn f s = eVariationOn f' s :=
  eq_of_edist_zero_on fun x xs => by rw [h xs, edist_self]

theorem sum_le (f : Î± â†’ E) {s : Set Î±} (n : â„•) {u : â„• â†’ Î±} (hu : Monotone u) (us : âˆ€ i, u i âˆˆ s) :
    (âˆ‘ i âˆˆ Finset.range n, edist (f (u (i + 1))) (f (u i))) â‰¤ eVariationOn f s :=
  le_iSup_of_le âŸ¨n, u, hu, usâŸ© le_rfl

theorem sum_le_of_monotoneOn_Icc (f : Î± â†’ E) {s : Set Î±} {m n : â„•} {u : â„• â†’ Î±}
    (hu : MonotoneOn u (Icc m n)) (us : âˆ€ i âˆˆ Icc m n, u i âˆˆ s) :
    (âˆ‘ i âˆˆ Finset.Ico m n, edist (f (u (i + 1))) (f (u i))) â‰¤ eVariationOn f s := by
  rcases le_total n m with hnm | hmn
  Â· simp [Finset.Ico_eq_empty_of_le hnm]
  let Ï€ := projIcc m n hmn
  let v i := u (Ï€ i)
  calc
    âˆ‘ i âˆˆ Finset.Ico m n, edist (f (u (i + 1))) (f (u i))
        = âˆ‘ i âˆˆ Finset.Ico m n, edist (f (v (i + 1))) (f (v i)) :=
      Finset.sum_congr rfl fun i hi â†¦ by
        rw [Finset.mem_Ico] at hi
        simp only [v, Ï€, projIcc_of_mem hmn âŸ¨hi.1, hi.2.leâŸ©,
          projIcc_of_mem hmn âŸ¨hi.1.trans i.le_succ, hi.2âŸ©]
    _ â‰¤ âˆ‘ i âˆˆ Finset.range n, edist (f (v (i + 1))) (f (v i)) :=
      Finset.sum_mono_set _ (Nat.Iio_eq_range â–¸ Finset.Ico_subset_Iio_self)
    _ â‰¤ eVariationOn f s :=
      sum_le _ _ (fun i j h â†¦ hu (Ï€ i).2 (Ï€ j).2 (monotone_projIcc hmn h)) fun i â†¦ us _ (Ï€ i).2

theorem sum_le_of_monotoneOn_Iic (f : Î± â†’ E) {s : Set Î±} {n : â„•} {u : â„• â†’ Î±}
    (hu : MonotoneOn u (Iic n)) (us : âˆ€ i â‰¤ n, u i âˆˆ s) :
    (âˆ‘ i âˆˆ Finset.range n, edist (f (u (i + 1))) (f (u i))) â‰¤ eVariationOn f s := by
  simpa using sum_le_of_monotoneOn_Icc f (m := 0) (hu.mono Icc_subset_Iic_self) fun i hi â†¦ us i hi.2

theorem mono (f : Î± â†’ E) {s t : Set Î±} (hst : t âŠ† s) : eVariationOn f t â‰¤ eVariationOn f s := by
  apply iSup_le _
  rintro âŸ¨n, âŸ¨u, hu, utâŸ©âŸ©
  exact sum_le f n hu fun i => hst (ut i)

theorem _root_.BoundedVariationOn.mono {f : Î± â†’ E} {s : Set Î±} (h : BoundedVariationOn f s)
    {t : Set Î±} (ht : t âŠ† s) : BoundedVariationOn f t :=
  ne_top_of_le_ne_top h (eVariationOn.mono f ht)

theorem _root_.BoundedVariationOn.locallyBoundedVariationOn {f : Î± â†’ E} {s : Set Î±}
    (h : BoundedVariationOn f s) : LocallyBoundedVariationOn f s := fun _ _ _ _ =>
  h.mono inter_subset_left

theorem edist_le (f : Î± â†’ E) {s : Set Î±} {x y : Î±} (hx : x âˆˆ s) (hy : y âˆˆ s) :
    edist (f x) (f y) â‰¤ eVariationOn f s := by
  wlog hxy : y â‰¤ x generalizing x y
  Â· rw [edist_comm]
    exact this hy hx (le_of_not_ge hxy)
  let u : â„• â†’ Î± := fun n => if n = 0 then y else x
  have hu : Monotone u := monotone_nat_of_le_succ fun
  | 0 => hxy
  | (_ + 1) => le_rfl
  have us : âˆ€ i, u i âˆˆ s := fun
  | 0 => hy
  | (_ + 1) => hx
  simpa only [Finset.sum_range_one] using sum_le f 1 hu us

theorem eq_zero_iff (f : Î± â†’ E) {s : Set Î±} :
    eVariationOn f s = 0 â†” âˆ€ x âˆˆ s, âˆ€ y âˆˆ s, edist (f x) (f y) = 0 := by
  constructor
  Â· rintro h x xs y ys
    rw [â† le_zero_iff, â† h]
    exact edist_le f xs ys
  Â· rintro h
    dsimp only [eVariationOn]
    rw [ENNReal.iSup_eq_zero]
    rintro âŸ¨n, u, um, usâŸ©
    exact Finset.sum_eq_zero fun i _ => h _ (us i.succ) _ (us i)

theorem constant_on {f : Î± â†’ E} {s : Set Î±} (hf : (f '' s).Subsingleton) :
    eVariationOn f s = 0 := by
  rw [eq_zero_iff]
  rintro x xs y ys
  rw [hf âŸ¨x, xs, rflâŸ© âŸ¨y, ys, rflâŸ©, edist_self]

@[simp]
protected theorem subsingleton (f : Î± â†’ E) {s : Set Î±} (hs : s.Subsingleton) :
    eVariationOn f s = 0 :=
  constant_on (hs.image f)

theorem lowerSemicontinuous_aux {Î¹ : Type*} {F : Î¹ â†’ Î± â†’ E} {p : Filter Î¹} {f : Î± â†’ E} {s : Set Î±}
    (Ffs : âˆ€ x âˆˆ s, Tendsto (fun i => F i x) p (ğ“ (f x))) {v : â„â‰¥0âˆ} (hv : v < eVariationOn f s) :
    âˆ€á¶  n : Î¹ in p, v < eVariationOn (F n) s := by
  obtain âŸ¨âŸ¨n, âŸ¨u, um, usâŸ©âŸ©, hltâŸ© :
    âˆƒ p : â„• Ã— { u : â„• â†’ Î± // Monotone u âˆ§ âˆ€ i, u i âˆˆ s },
      v < âˆ‘ i âˆˆ Finset.range p.1, edist (f ((p.2 : â„• â†’ Î±) (i + 1))) (f ((p.2 : â„• â†’ Î±) i)) :=
    lt_iSup_iff.mp hv
  have : Tendsto (fun j => âˆ‘ i âˆˆ Finset.range n, edist (F j (u (i + 1))) (F j (u i))) p
      (ğ“ (âˆ‘ i âˆˆ Finset.range n, edist (f (u (i + 1))) (f (u i)))) := by
    apply tendsto_finset_sum
    exact fun i _ => Tendsto.edist (Ffs (u i.succ) (us i.succ)) (Ffs (u i) (us i))
  exact (this.eventually_const_lt hlt).mono fun i h => h.trans_le (sum_le (F i) n um us)

/-- The map `(eVariationOn Â· s)` is lower semicontinuous for pointwise convergence *on `s`*.
Pointwise convergence on `s` is encoded here as uniform convergence on the family consisting of the
singletons of elements of `s`.
-/
protected theorem lowerSemicontinuous (s : Set Î±) :
    LowerSemicontinuous fun f : Î± â†’áµ¤[s.image singleton] E => eVariationOn f s := fun f â†¦ by
  apply @lowerSemicontinuous_aux _ _ _ _ (UniformOnFun Î± E (s.image singleton)) id (ğ“ f) f s _
  simpa only [UniformOnFun.tendsto_iff_tendstoUniformlyOn, mem_image, forall_exists_index, and_imp,
    forall_apply_eq_imp_iffâ‚‚, tendstoUniformlyOn_singleton_iff_tendsto] using @tendsto_id _ (ğ“ f)

/-- The map `(eVariationOn Â· s)` is lower semicontinuous for uniform convergence on `s`. -/
theorem lowerSemicontinuous_uniformOn (s : Set Î±) :
    LowerSemicontinuous fun f : Î± â†’áµ¤[{s}] E => eVariationOn f s := fun f â†¦ by
  apply @lowerSemicontinuous_aux _ _ _ _ (UniformOnFun Î± E {s}) id (ğ“ f) f s _
  have := @tendsto_id _ (ğ“ f)
  rw [UniformOnFun.tendsto_iff_tendstoUniformlyOn] at this
  simp_rw [â† tendstoUniformlyOn_singleton_iff_tendsto]
  exact fun x xs => (this s rfl).mono (singleton_subset_iff.mpr xs)

theorem _root_.BoundedVariationOn.dist_le {E : Type*} [PseudoMetricSpace E] {f : Î± â†’ E}
    {s : Set Î±} (h : BoundedVariationOn f s) {x y : Î±} (hx : x âˆˆ s) (hy : y âˆˆ s) :
    dist (f x) (f y) â‰¤ (eVariationOn f s).toReal := by
  rw [â† ENNReal.ofReal_le_ofReal_iff ENNReal.toReal_nonneg, ENNReal.ofReal_toReal h, â† edist_dist]
  exact edist_le f hx hy

theorem _root_.BoundedVariationOn.sub_le {f : Î± â†’ â„} {s : Set Î±} (h : BoundedVariationOn f s)
    {x y : Î±} (hx : x âˆˆ s) (hy : y âˆˆ s) : f x - f y â‰¤ (eVariationOn f s).toReal := by
  apply (le_abs_self _).trans
  rw [â† Real.dist_eq]
  exact h.dist_le hx hy

/-- Consider a monotone function `u` parameterizing some points of a set `s`. Given `x âˆˆ s`, then
one can find another monotone function `v` parameterizing the same points as `u`, with `x` added.
In particular, the variation of a function along `u` is bounded by its variation along `v`. -/
theorem add_point (f : Î± â†’ E) {s : Set Î±} {x : Î±} (hx : x âˆˆ s) (u : â„• â†’ Î±) (hu : Monotone u)
    (us : âˆ€ i, u i âˆˆ s) (n : â„•) :
    âˆƒ (v : â„• â†’ Î±) (m : â„•), Monotone v âˆ§ (âˆ€ i, v i âˆˆ s) âˆ§ x âˆˆ v '' Iio m âˆ§
      (âˆ‘ i âˆˆ Finset.range n, edist (f (u (i + 1))) (f (u i))) â‰¤
        âˆ‘ j âˆˆ Finset.range m, edist (f (v (j + 1))) (f (v j)) := by
  rcases le_or_gt (u n) x with (h | h)
  Â· let v i := if i â‰¤ n then u i else x
    have vs : âˆ€ i, v i âˆˆ s := fun i â†¦ by
      simp only [v]
      split_ifs
      Â· exact us i
      Â· exact hx
    have hv : Monotone v := by
      refine monotone_nat_of_le_succ fun i => ?_
      simp only [v]
      rcases lt_trichotomy i n with (hi | rfl | hi)
      Â· have : i + 1 â‰¤ n := Nat.succ_le_of_lt hi
        simp only [hi.le, this, if_true]
        exact hu (Nat.le_succ i)
      Â· simp only [le_refl, if_true, add_le_iff_nonpos_right, Nat.le_zero, Nat.one_ne_zero,
          if_false, h]
      Â· have A : Â¬i â‰¤ n := hi.not_ge
        have B : Â¬i + 1 â‰¤ n := fun h => A (i.le_succ.trans h)
        simp only [A, B, if_false, le_rfl]
    refine âŸ¨v, n + 2, hv, vs, (mem_image _ _ _).2 âŸ¨n + 1, ?_, ?_âŸ©, ?_âŸ©
    Â· rw [mem_Iio]; exact Nat.lt_succ_self (n + 1)
    Â· have : Â¬n + 1 â‰¤ n := Nat.not_succ_le_self n
      simp only [v, this, ite_eq_right_iff, IsEmpty.forall_iff]
    Â· calc
        (âˆ‘ i âˆˆ Finset.range n, edist (f (u (i + 1))) (f (u i))) =
            âˆ‘ i âˆˆ Finset.range n, edist (f (v (i + 1))) (f (v i)) := by
          apply Finset.sum_congr rfl fun i hi => ?_
          simp only [Finset.mem_range] at hi
          have : i + 1 â‰¤ n := Nat.succ_le_of_lt hi
          simp only [v, hi.le, this, if_true]
        _ â‰¤ âˆ‘ j âˆˆ Finset.range (n + 2), edist (f (v (j + 1))) (f (v j)) := by
          gcongr
          apply Nat.le_add_right
  have exists_N : âˆƒ N, N â‰¤ n âˆ§ x < u N := âŸ¨n, le_rfl, hâŸ©
  let N := Nat.find exists_N
  have hN : N â‰¤ n âˆ§ x < u N := Nat.find_spec exists_N
  let w : â„• â†’ Î± := fun i => if i < N then u i else if i = N then x else u (i - 1)
  have ws : âˆ€ i, w i âˆˆ s := by grind
  have hw : Monotone w := by
    apply monotone_nat_of_le_succ fun i => ?_
    dsimp only [w]
    rcases lt_trichotomy (i + 1) N with (hi | hi | hi)
    Â· have : i < N := Nat.lt_of_le_of_lt (Nat.le_succ i) hi
      simp only [hi, this, if_true]
      exact hu (Nat.le_succ _)
    Â· have A : i < N := hi â–¸ i.lt_succ_self
      have B : Â¬i + 1 < N := by rw [â† hi]; exact fun h => h.ne rfl
      rw [if_pos A, if_neg B, if_pos hi]
      have T := Nat.find_min exists_N A
      push_neg at T
      exact T (A.le.trans hN.1)
    Â· have A : Â¬i < N := (Nat.lt_succ_iff.mp hi).not_gt
      have B : Â¬i + 1 < N := hi.not_gt
      have C : Â¬i + 1 = N := hi.ne.symm
      have D : i + 1 - 1 = i := Nat.pred_succ i
      rw [if_neg A, if_neg B, if_neg C, D]
      split_ifs
      Â· exact hN.2.le.trans (hu (le_of_not_gt A))
      Â· exact hu (Nat.pred_le _)
  refine âŸ¨w, n + 1, hw, ws, (mem_image _ _ _).2 âŸ¨N, hN.1.trans_lt (Nat.lt_succ_self n), ?_âŸ©, ?_âŸ©
  Â· dsimp only [w]; rw [if_neg (lt_irrefl N), if_pos rfl]
  rcases eq_or_lt_of_le (zero_le N) with (Npos | Npos)
  Â· calc
      (âˆ‘ i âˆˆ Finset.range n, edist (f (u (i + 1))) (f (u i))) =
          âˆ‘ i âˆˆ Finset.range n, edist (f (w (1 + i + 1))) (f (w (1 + i))) := by grind
      _ = âˆ‘ i âˆˆ Finset.Ico 1 (n + 1), edist (f (w (i + 1))) (f (w i)) := by
        rw [Finset.range_eq_Ico]
        exact Finset.sum_Ico_add (fun i => edist (f (w (i + 1))) (f (w i))) 0 n 1
      _ â‰¤ âˆ‘ j âˆˆ Finset.range (n + 1), edist (f (w (j + 1))) (f (w j)) := by
        rw [Finset.range_eq_Ico]
        gcongr
        exact zero_le_one
  Â· calc
      (âˆ‘ i âˆˆ Finset.range n, edist (f (u (i + 1))) (f (u i))) =
          ((âˆ‘ i âˆˆ Finset.Ico 0 (N - 1), edist (f (u (i + 1))) (f (u i))) +
              âˆ‘ i âˆˆ Finset.Ico (N - 1) N, edist (f (u (i + 1))) (f (u i))) +
            âˆ‘ i âˆˆ Finset.Ico N n, edist (f (u (i + 1))) (f (u i)) := by
        rw [Finset.sum_Ico_consecutive, Finset.sum_Ico_consecutive, Finset.range_eq_Ico]
        Â· exact zero_le _
        Â· exact hN.1
        Â· exact zero_le _
        Â· exact Nat.pred_le _
      _ = (âˆ‘ i âˆˆ Finset.Ico 0 (N - 1), edist (f (w (i + 1))) (f (w i))) +
              edist (f (u N)) (f (u (N - 1))) +
            âˆ‘ i âˆˆ Finset.Ico N n, edist (f (w (1 + i + 1))) (f (w (1 + i))) := by
        congr 1
        Â· congr 1
          Â· grind [Finset.mem_Ico, Finset.sum_congr]
          Â· have A : N - 1 + 1 = N := Nat.succ_pred_eq_of_pos Npos
            have : Finset.Ico (N - 1) N = {N - 1} := by rw [â† Nat.Ico_succ_singleton, A]
            simp only [this, A, Finset.sum_singleton]
        Â· grind [Finset.sum_congr, Finset.mem_Ico]
      _ = (âˆ‘ i âˆˆ Finset.Ico 0 (N - 1), edist (f (w (i + 1))) (f (w i))) +
              edist (f (w (N + 1))) (f (w (N - 1))) +
            âˆ‘ i âˆˆ Finset.Ico (N + 1) (n + 1), edist (f (w (i + 1))) (f (w i)) := by
        congr 1
        Â· grind
        Â· exact Finset.sum_Ico_add (fun i => edist (f (w (i + 1))) (f (w i))) N n 1
      _ â‰¤ ((âˆ‘ i âˆˆ Finset.Ico 0 (N - 1), edist (f (w (i + 1))) (f (w i))) +
              âˆ‘ i âˆˆ Finset.Ico (N - 1) (N + 1), edist (f (w (i + 1))) (f (w i))) +
            âˆ‘ i âˆˆ Finset.Ico (N + 1) (n + 1), edist (f (w (i + 1))) (f (w i)) := by
        refine add_le_add (add_le_add le_rfl ?_) le_rfl
        have A : N - 1 + 1 = N := Nat.succ_pred_eq_of_pos Npos
        have B : N - 1 + 1 < N + 1 := A.symm â–¸ N.lt_succ_self
        have C : N - 1 < N + 1 := lt_of_le_of_lt N.pred_le N.lt_succ_self
        rw [Finset.sum_eq_sum_Ico_succ_bot C, Finset.sum_eq_sum_Ico_succ_bot B, A, Finset.Ico_self,
          Finset.sum_empty, add_zero, add_comm (edist _ _)]
        exact edist_triangle _ _ _
      _ = âˆ‘ j âˆˆ Finset.range (n + 1), edist (f (w (j + 1))) (f (w j)) := by
        rw [Finset.sum_Ico_consecutive, Finset.sum_Ico_consecutive, Finset.range_eq_Ico]
        Â· exact zero_le _
        Â· exact Nat.succ_le_succ hN.left
        Â· exact zero_le _
        Â· exact N.pred_le.trans N.le_succ

/-- The variation of a function on the union of two sets `s` and `t`, with `s` to the left of `t`,
bounds the sum of the variations along `s` and `t`. -/
theorem add_le_union (f : Î± â†’ E) {s t : Set Î±} (h : âˆ€ x âˆˆ s, âˆ€ y âˆˆ t, x â‰¤ y) :
    eVariationOn f s + eVariationOn f t â‰¤ eVariationOn f (s âˆª t) := by
  by_cases hs : s = âˆ…
  Â· simp [hs]
  have : Nonempty { u // Monotone u âˆ§ âˆ€ i : â„•, u i âˆˆ s } :=
    nonempty_monotone_mem (nonempty_iff_ne_empty.2 hs)
  by_cases ht : t = âˆ…
  Â· simp [ht]
  have : Nonempty { u // Monotone u âˆ§ âˆ€ i : â„•, u i âˆˆ t } :=
    nonempty_monotone_mem (nonempty_iff_ne_empty.2 ht)
  refine ENNReal.iSup_add_iSup_le ?_
  /- We start from two sequences `u` and `v` along `s` and `t` respectively, and we build a new
    sequence `w` along `s âˆª t` by juxtaposing them. Its variation is larger than the sum of the
    variations. -/
  rintro âŸ¨n, âŸ¨u, hu, usâŸ©âŸ© âŸ¨m, âŸ¨v, hv, vtâŸ©âŸ©
  let w i := if i â‰¤ n then u i else v (i - (n + 1))
  have wst : âˆ€ i, w i âˆˆ s âˆª t := by
    intro i
    by_cases hi : i â‰¤ n
    Â· simp [w, hi, us]
    Â· simp [w, hi, vt]
  have hw : Monotone w := by
    intro i j hij
    dsimp only [w]
    split_ifs with h_1 h_2 h_2
    Â· exact hu hij
    Â· apply h _ (us _) _ (vt _)
    Â· exfalso; exact h_1 (hij.trans h_2)
    Â· apply hv (tsub_le_tsub hij le_rfl)
  calc
    ((âˆ‘ i âˆˆ Finset.range n, edist (f (u (i + 1))) (f (u i))) +
          âˆ‘ i âˆˆ Finset.range m, edist (f (v (i + 1))) (f (v i))) =
        (âˆ‘ i âˆˆ Finset.range n, edist (f (w (i + 1))) (f (w i))) +
          âˆ‘ i âˆˆ Finset.range m, edist (f (w (n + 1 + i + 1))) (f (w (n + 1 + i))) := by
      dsimp only [w]
      congr 1
      Â· grind [Finset.sum_congr]
      Â· grind
    _ = (âˆ‘ i âˆˆ Finset.range n, edist (f (w (i + 1))) (f (w i))) +
          âˆ‘ i âˆˆ Finset.Ico (n + 1) (n + 1 + m), edist (f (w (i + 1))) (f (w i)) := by
      congr 1
      rw [Finset.range_eq_Ico]
      convert Finset.sum_Ico_add (fun i : â„• => edist (f (w (i + 1))) (f (w i))) 0 m (n + 1)
        using 3 <;> abel
    _ â‰¤ âˆ‘ i âˆˆ Finset.range (n + 1 + m), edist (f (w (i + 1))) (f (w i)) := by
      rw [â† Finset.sum_union]
      Â· gcongr
        rintro i hi
        simp only [Finset.mem_union, Finset.mem_range, Finset.mem_Ico] at hi âŠ¢
        omega
      Â· refine Finset.disjoint_left.2 fun i hi h'i => ?_
        simp only [Finset.mem_Ico, Finset.mem_range] at hi h'i
        exact hi.not_gt (Nat.lt_of_succ_le h'i.left)
    _ â‰¤ eVariationOn f (s âˆª t) := sum_le f _ hw wst

/-- If a set `s` is to the left of a set `t`, and both contain the boundary point `x`, then
the variation of `f` along `s âˆª t` is the sum of the variations. -/
theorem union (f : Î± â†’ E) {s t : Set Î±} {x : Î±} (hs : IsGreatest s x) (ht : IsLeast t x) :
    eVariationOn f (s âˆª t) = eVariationOn f s + eVariationOn f t := by
  classical
  apply le_antisymm _ (eVariationOn.add_le_union f fun a ha b hb => le_trans (hs.2 ha) (ht.2 hb))
  apply iSup_le _
  rintro âŸ¨n, âŸ¨u, hu, ustâŸ©âŸ©
  obtain âŸ¨v, m, hv, vst, xv, huvâŸ© : âˆƒ (v : â„• â†’ Î±) (m : â„•),
    Monotone v âˆ§ (âˆ€ i, v i âˆˆ s âˆª t) âˆ§ x âˆˆ v '' Iio m âˆ§
      (âˆ‘ i âˆˆ Finset.range n, edist (f (u (i + 1))) (f (u i))) â‰¤
        âˆ‘ j âˆˆ Finset.range m, edist (f (v (j + 1))) (f (v j)) :=
    eVariationOn.add_point f (mem_union_left t hs.1) u hu ust n
  obtain âŸ¨N, hN, NxâŸ© : âˆƒ N, N < m âˆ§ v N = x := xv
  calc
    (âˆ‘ j âˆˆ Finset.range n, edist (f (u (j + 1))) (f (u j))) â‰¤
        âˆ‘ j âˆˆ Finset.range m, edist (f (v (j + 1))) (f (v j)) :=
      huv
    _ = (âˆ‘ j âˆˆ Finset.Ico 0 N, edist (f (v (j + 1))) (f (v j))) +
          âˆ‘ j âˆˆ Finset.Ico N m, edist (f (v (j + 1))) (f (v j)) := by
      rw [Finset.range_eq_Ico, Finset.sum_Ico_consecutive _ (zero_le _) hN.le]
    _ â‰¤ eVariationOn f s + eVariationOn f t := by
      refine add_le_add ?_ ?_
      Â· apply sum_le_of_monotoneOn_Icc _ (hv.monotoneOn _) fun i hi => ?_
        rcases vst i with (h | h); Â· exact h
        have : v i = x := by
          apply le_antisymm
          Â· rw [â† Nx]; exact hv hi.2
          Â· exact ht.2 h
        rw [this]
        exact hs.1
      Â· apply sum_le_of_monotoneOn_Icc _ (hv.monotoneOn _) fun i hi => ?_
        rcases vst i with (h | h); swap; Â· exact h
        have : v i = x := by
          apply le_antisymm
          Â· exact hs.2 h
          Â· rw [â† Nx]; exact hv hi.1
        rw [this]
        exact ht.1

theorem Icc_add_Icc (f : Î± â†’ E) {s : Set Î±} {a b c : Î±} (hab : a â‰¤ b) (hbc : b â‰¤ c) (hb : b âˆˆ s) :
    eVariationOn f (s âˆ© Icc a b) + eVariationOn f (s âˆ© Icc b c) = eVariationOn f (s âˆ© Icc a c) := by
  have A : IsGreatest (s âˆ© Icc a b) b :=
    âŸ¨âŸ¨hb, hab, le_rflâŸ©, inter_subset_right.trans Icc_subset_Iic_selfâŸ©
  have B : IsLeast (s âˆ© Icc b c) b :=
    âŸ¨âŸ¨hb, le_rfl, hbcâŸ©, inter_subset_right.trans Icc_subset_Ici_selfâŸ©
  rw [â† eVariationOn.union f A B, â† inter_union_distrib_left, Icc_union_Icc_eq_Icc hab hbc]

section Monotone

variable {Î² : Type*} [LinearOrder Î²]

theorem comp_le_of_monotoneOn (f : Î± â†’ E) {s : Set Î±} {t : Set Î²} (Ï† : Î² â†’ Î±) (hÏ† : MonotoneOn Ï† t)
    (Ï†st : MapsTo Ï† t s) : eVariationOn (f âˆ˜ Ï†) t â‰¤ eVariationOn f s :=
  iSup_le fun âŸ¨n, u, hu, utâŸ© =>
    le_iSup_of_le âŸ¨n, Ï† âˆ˜ u, fun x y xy => hÏ† (ut x) (ut y) (hu xy), fun i => Ï†st (ut i)âŸ© le_rfl

theorem comp_le_of_antitoneOn (f : Î± â†’ E) {s : Set Î±} {t : Set Î²} (Ï† : Î² â†’ Î±) (hÏ† : AntitoneOn Ï† t)
    (Ï†st : MapsTo Ï† t s) : eVariationOn (f âˆ˜ Ï†) t â‰¤ eVariationOn f s := by
  refine iSup_le ?_
  rintro âŸ¨n, u, hu, utâŸ©
  rw [â† Finset.sum_range_reflect]
  refine (Finset.sum_congr rfl fun x hx => ?_).trans_le <| le_iSup_of_le
    âŸ¨n, fun i => Ï† (u <| n - i), fun x y xy => hÏ† (ut _) (ut _) (hu <| Nat.sub_le_sub_left xy n),
      fun i => Ï†st (ut _)âŸ©
    le_rfl
  rw [Finset.mem_range] at hx
  dsimp only [Subtype.coe_mk, Function.comp_apply]
  rw [edist_comm]
  congr 4 <;> omega

theorem comp_eq_of_monotoneOn (f : Î± â†’ E) {t : Set Î²} (Ï† : Î² â†’ Î±) (hÏ† : MonotoneOn Ï† t) :
    eVariationOn (f âˆ˜ Ï†) t = eVariationOn f (Ï† '' t) := by
  apply le_antisymm (comp_le_of_monotoneOn f Ï† hÏ† (mapsTo_image Ï† t))
  cases isEmpty_or_nonempty Î²
  Â· convert zero_le (_ : â„â‰¥0âˆ)
    exact eVariationOn.subsingleton f <|
      (subsingleton_of_subsingleton.image _).anti (surjOn_image Ï† t)
  let Ïˆ := Ï†.invFunOn t
  have ÏˆÏ†s : EqOn (Ï† âˆ˜ Ïˆ) id (Ï† '' t) := (surjOn_image Ï† t).rightInvOn_invFunOn
  have Ïˆts : MapsTo Ïˆ (Ï† '' t) t := (surjOn_image Ï† t).mapsTo_invFunOn
  have hÏˆ : MonotoneOn Ïˆ (Ï† '' t) := Function.monotoneOn_of_rightInvOn_of_mapsTo hÏ† ÏˆÏ†s Ïˆts
  change eVariationOn (f âˆ˜ id) (Ï† '' t) â‰¤ eVariationOn (f âˆ˜ Ï†) t
  rw [â† eq_of_eqOn (ÏˆÏ†s.comp_left : EqOn (f âˆ˜ Ï† âˆ˜ Ïˆ) (f âˆ˜ id) (Ï† '' t))]
  exact comp_le_of_monotoneOn _ Ïˆ hÏˆ Ïˆts

theorem comp_inter_Icc_eq_of_monotoneOn (f : Î± â†’ E) {t : Set Î²} (Ï† : Î² â†’ Î±) (hÏ† : MonotoneOn Ï† t)
    {x y : Î²} (hx : x âˆˆ t) (hy : y âˆˆ t) :
    eVariationOn (f âˆ˜ Ï†) (t âˆ© Icc x y) = eVariationOn f (Ï† '' t âˆ© Icc (Ï† x) (Ï† y)) := by
  rcases le_total x y with (h | h)
  Â· convert comp_eq_of_monotoneOn f Ï† (hÏ†.mono Set.inter_subset_left)
    apply le_antisymm
    Â· rintro _ âŸ¨âŸ¨u, us, rflâŸ©, vÏ†x, vÏ†yâŸ©
      rcases le_total x u with (xu | ux)
      Â· rcases le_total u y with (uy | yu)
        Â· exact âŸ¨u, âŸ¨us, âŸ¨xu, uyâŸ©âŸ©, rflâŸ©
        Â· rw [le_antisymm vÏ†y (hÏ† hy us yu)]
          exact âŸ¨y, âŸ¨hy, âŸ¨h, le_rflâŸ©âŸ©, rflâŸ©
      Â· rw [â† le_antisymm vÏ†x (hÏ† us hx ux)]
        exact âŸ¨x, âŸ¨hx, âŸ¨le_rfl, hâŸ©âŸ©, rflâŸ©
    Â· rintro _ âŸ¨u, âŸ¨âŸ¨hu, xu, uyâŸ©, rflâŸ©âŸ©
      exact âŸ¨âŸ¨u, hu, rflâŸ©, âŸ¨hÏ† hx hu xu, hÏ† hu hy uyâŸ©âŸ©
  Â· rw [eVariationOn.subsingleton, eVariationOn.subsingleton]
    exacts [(Set.subsingleton_Icc_of_ge (hÏ† hy hx h)).anti Set.inter_subset_right,
      (Set.subsingleton_Icc_of_ge h).anti Set.inter_subset_right]

theorem comp_eq_of_antitoneOn (f : Î± â†’ E) {t : Set Î²} (Ï† : Î² â†’ Î±) (hÏ† : AntitoneOn Ï† t) :
    eVariationOn (f âˆ˜ Ï†) t = eVariationOn f (Ï† '' t) := by
  apply le_antisymm (comp_le_of_antitoneOn f Ï† hÏ† (mapsTo_image Ï† t))
  cases isEmpty_or_nonempty Î²
  Â· convert zero_le (_ : â„â‰¥0âˆ)
    exact eVariationOn.subsingleton f <| (subsingleton_of_subsingleton.image _).anti
      (surjOn_image Ï† t)
  let Ïˆ := Ï†.invFunOn t
  have ÏˆÏ†s : EqOn (Ï† âˆ˜ Ïˆ) id (Ï† '' t) := (surjOn_image Ï† t).rightInvOn_invFunOn
  have Ïˆts := (surjOn_image Ï† t).mapsTo_invFunOn
  have hÏˆ : AntitoneOn Ïˆ (Ï† '' t) := Function.antitoneOn_of_rightInvOn_of_mapsTo hÏ† ÏˆÏ†s Ïˆts
  change eVariationOn (f âˆ˜ id) (Ï† '' t) â‰¤ eVariationOn (f âˆ˜ Ï†) t
  rw [â† eq_of_eqOn (ÏˆÏ†s.comp_left : EqOn (f âˆ˜ Ï† âˆ˜ Ïˆ) (f âˆ˜ id) (Ï† '' t))]
  exact comp_le_of_antitoneOn _ Ïˆ hÏˆ Ïˆts

open OrderDual

theorem comp_ofDual (f : Î± â†’ E) (s : Set Î±) :
    eVariationOn (f âˆ˜ ofDual) (ofDual â»Â¹' s) = eVariationOn f s := by
  convert comp_eq_of_antitoneOn f ofDual fun _ _ _ _ => id
  simp only [Equiv.image_preimage]

end Monotone

end eVariationOn

/-! ## Monotone functions and bounded variation -/

theorem MonotoneOn.eVariationOn_le {f : Î± â†’ â„} {s : Set Î±} (hf : MonotoneOn f s) {a b : Î±}
    (as : a âˆˆ s) (bs : b âˆˆ s) : eVariationOn f (s âˆ© Icc a b) â‰¤ ENNReal.ofReal (f b - f a) := by
  apply iSup_le _
  rintro âŸ¨n, âŸ¨u, hu, usâŸ©âŸ©
  calc
    (âˆ‘ i âˆˆ Finset.range n, edist (f (u (i + 1))) (f (u i))) =
        âˆ‘ i âˆˆ Finset.range n, ENNReal.ofReal (f (u (i + 1)) - f (u i)) := by
      refine Finset.sum_congr rfl fun i hi => ?_
      simp only [Finset.mem_range] at hi
      rw [edist_dist, Real.dist_eq, abs_of_nonneg]
      exact sub_nonneg_of_le (hf (us i).1 (us (i + 1)).1 (hu (Nat.le_succ _)))
    _ = ENNReal.ofReal (âˆ‘ i âˆˆ Finset.range n, (f (u (i + 1)) - f (u i))) := by
      rw [ENNReal.ofReal_sum_of_nonneg]
      intro i _
      exact sub_nonneg_of_le (hf (us i).1 (us (i + 1)).1 (hu (Nat.le_succ _)))
    _ = ENNReal.ofReal (f (u n) - f (u 0)) := by rw [Finset.sum_range_sub fun i => f (u i)]
    _ â‰¤ ENNReal.ofReal (f b - f a) := by
      apply ENNReal.ofReal_le_ofReal
      exact sub_le_sub (hf (us n).1 bs (us n).2.2) (hf as (us 0).1 (us 0).2.1)

theorem MonotoneOn.locallyBoundedVariationOn {f : Î± â†’ â„} {s : Set Î±} (hf : MonotoneOn f s) :
    LocallyBoundedVariationOn f s := fun _ _ as bs =>
  ((hf.eVariationOn_le as bs).trans_lt ENNReal.ofReal_lt_top).ne

/-- The **signed** variation of `f` on the interval `Icc a b` intersected with the set `s`,
squashed to a real (therefore only really meaningful if the variation is finite)
-/
noncomputable def variationOnFromTo (f : Î± â†’ E) (s : Set Î±) (a b : Î±) : â„ :=
  if a â‰¤ b then (eVariationOn f (s âˆ© Icc a b)).toReal else -(eVariationOn f (s âˆ© Icc b a)).toReal

namespace variationOnFromTo

variable (f : Î± â†’ E) (s : Set Î±)

protected theorem self (a : Î±) : variationOnFromTo f s a a = 0 := by
  dsimp only [variationOnFromTo]
  rw [if_pos le_rfl, Icc_self, eVariationOn.subsingleton, ENNReal.toReal_zero]
  exact fun x hx y hy => hx.2.trans hy.2.symm

protected theorem nonneg_of_le {a b : Î±} (h : a â‰¤ b) : 0 â‰¤ variationOnFromTo f s a b := by
  simp only [variationOnFromTo, if_pos h, ENNReal.toReal_nonneg]

protected theorem eq_neg_swap (a b : Î±) :
    variationOnFromTo f s a b = -variationOnFromTo f s b a := by
  rcases lt_trichotomy a b with (ab | rfl | ba)
  Â· simp only [variationOnFromTo, if_pos ab.le, if_neg ab.not_ge, neg_neg]
  Â· simp only [variationOnFromTo.self, neg_zero]
  Â· simp only [variationOnFromTo, if_pos ba.le, if_neg ba.not_ge]

protected theorem nonpos_of_ge {a b : Î±} (h : b â‰¤ a) : variationOnFromTo f s a b â‰¤ 0 := by
  rw [variationOnFromTo.eq_neg_swap]
  exact neg_nonpos_of_nonneg (variationOnFromTo.nonneg_of_le f s h)

protected theorem eq_of_le {a b : Î±} (h : a â‰¤ b) :
    variationOnFromTo f s a b = (eVariationOn f (s âˆ© Icc a b)).toReal :=
  if_pos h

protected theorem eq_of_ge {a b : Î±} (h : b â‰¤ a) :
    variationOnFromTo f s a b = -(eVariationOn f (s âˆ© Icc b a)).toReal := by
  rw [variationOnFromTo.eq_neg_swap, neg_inj, variationOnFromTo.eq_of_le f s h]

protected theorem add {f : Î± â†’ E} {s : Set Î±} (hf : LocallyBoundedVariationOn f s) {a b c : Î±}
    (ha : a âˆˆ s) (hb : b âˆˆ s) (hc : c âˆˆ s) :
    variationOnFromTo f s a b + variationOnFromTo f s b c = variationOnFromTo f s a c := by
  symm
  refine additive_of_isTotal (Â· â‰¤ Â· : Î± â†’ Î± â†’ Prop) (variationOnFromTo f s) (Â· âˆˆ s) ?_ ?_ ha hb hc
  Â· rintro x y _xs _ys
    simp only [variationOnFromTo.eq_neg_swap f s y x, add_neg_cancel]
  Â· rintro x y z xy yz xs ys zs
    rw [variationOnFromTo.eq_of_le f s xy, variationOnFromTo.eq_of_le f s yz,
      variationOnFromTo.eq_of_le f s (xy.trans yz),
      â† ENNReal.toReal_add (hf x y xs ys) (hf y z ys zs), eVariationOn.Icc_add_Icc f xy yz ys]

variable {f s} in
protected theorem edist_zero_of_eq_zero (hf : LocallyBoundedVariationOn f s)
    {a b : Î±} (ha : a âˆˆ s) (hb : b âˆˆ s) (h : variationOnFromTo f s a b = 0) :
    edist (f a) (f b) = 0 := by
  wlog h' : a â‰¤ b
  Â· rw [edist_comm]
    apply this hf hb ha _ (le_of_not_ge h')
    rw [variationOnFromTo.eq_neg_swap, h, neg_zero]
  Â· apply le_antisymm _ (zero_le _)
    rw [â† ENNReal.ofReal_zero, â† h, variationOnFromTo.eq_of_le f s h',
      ENNReal.ofReal_toReal (hf a b ha hb)]
    apply eVariationOn.edist_le
    exacts [âŸ¨ha, âŸ¨le_rfl, h'âŸ©âŸ©, âŸ¨hb, âŸ¨h', le_rflâŸ©âŸ©]

protected theorem eq_left_iff {f : Î± â†’ E} {s : Set Î±} (hf : LocallyBoundedVariationOn f s)
    {a b c : Î±} (ha : a âˆˆ s) (hb : b âˆˆ s) (hc : c âˆˆ s) :
    variationOnFromTo f s a b = variationOnFromTo f s a c â†” variationOnFromTo f s b c = 0 := by
  simp only [â† variationOnFromTo.add hf ha hb hc, left_eq_add]

protected theorem eq_zero_iff_of_le {f : Î± â†’ E} {s : Set Î±} (hf : LocallyBoundedVariationOn f s)
    {a b : Î±} (ha : a âˆˆ s) (hb : b âˆˆ s) (ab : a â‰¤ b) :
    variationOnFromTo f s a b = 0 â†”
      âˆ€ â¦ƒxâ¦„ (_hx : x âˆˆ s âˆ© Icc a b) â¦ƒyâ¦„ (_hy : y âˆˆ s âˆ© Icc a b), edist (f x) (f y) = 0 := by
  rw [variationOnFromTo.eq_of_le _ _ ab, ENNReal.toReal_eq_zero_iff, or_iff_left (hf a b ha hb),
    eVariationOn.eq_zero_iff]

protected theorem eq_zero_iff_of_ge {f : Î± â†’ E} {s : Set Î±} (hf : LocallyBoundedVariationOn f s)
    {a b : Î±} (ha : a âˆˆ s) (hb : b âˆˆ s) (ba : b â‰¤ a) :
    variationOnFromTo f s a b = 0 â†”
      âˆ€ â¦ƒxâ¦„ (_hx : x âˆˆ s âˆ© Icc b a) â¦ƒyâ¦„ (_hy : y âˆˆ s âˆ© Icc b a), edist (f x) (f y) = 0 := by
  rw [variationOnFromTo.eq_of_ge _ _ ba, neg_eq_zero, ENNReal.toReal_eq_zero_iff,
    or_iff_left (hf b a hb ha), eVariationOn.eq_zero_iff]

protected theorem eq_zero_iff {f : Î± â†’ E} {s : Set Î±} (hf : LocallyBoundedVariationOn f s) {a b : Î±}
    (ha : a âˆˆ s) (hb : b âˆˆ s) :
    variationOnFromTo f s a b = 0 â†”
      âˆ€ â¦ƒxâ¦„ (_hx : x âˆˆ s âˆ© uIcc a b) â¦ƒyâ¦„ (_hy : y âˆˆ s âˆ© uIcc a b), edist (f x) (f y) = 0 := by
  rcases le_total a b with (ab | ba)
  Â· rw [uIcc_of_le ab]
    exact variationOnFromTo.eq_zero_iff_of_le hf ha hb ab
  Â· rw [uIcc_of_ge ba]
    exact variationOnFromTo.eq_zero_iff_of_ge hf ha hb ba

variable {f} {s}

protected theorem monotoneOn (hf : LocallyBoundedVariationOn f s) {a : Î±} (as : a âˆˆ s) :
    MonotoneOn (variationOnFromTo f s a) s := by
  rintro b bs c cs bc
  rw [â† variationOnFromTo.add hf as bs cs]
  exact le_add_of_nonneg_right (variationOnFromTo.nonneg_of_le f s bc)

protected theorem antitoneOn (hf : LocallyBoundedVariationOn f s) {b : Î±} (bs : b âˆˆ s) :
    AntitoneOn (fun a => variationOnFromTo f s a b) s := by
  rintro a as c cs ac
  dsimp only
  rw [â† variationOnFromTo.add hf as cs bs]
  exact le_add_of_nonneg_left (variationOnFromTo.nonneg_of_le f s ac)

protected theorem sub_self_monotoneOn {f : Î± â†’ â„} {s : Set Î±} (hf : LocallyBoundedVariationOn f s)
    {a : Î±} (as : a âˆˆ s) : MonotoneOn (variationOnFromTo f s a - f) s := by
  rintro b bs c cs bc
  rw [Pi.sub_apply, Pi.sub_apply, le_sub_iff_add_le, add_comm_sub, â† le_sub_iff_add_le']
  calc
    f c - f b â‰¤ |f c - f b| := le_abs_self _
    _ = dist (f b) (f c) := by rw [dist_comm, Real.dist_eq]
    _ â‰¤ variationOnFromTo f s b c := by
      rw [variationOnFromTo.eq_of_le f s bc, dist_edist]
      apply ENNReal.toReal_mono (hf b c bs cs)
      apply eVariationOn.edist_le f
      exacts [âŸ¨bs, le_rfl, bcâŸ©, âŸ¨cs, bc, le_rflâŸ©]
    _ = variationOnFromTo f s a c - variationOnFromTo f s a b := by
      rw [â† variationOnFromTo.add hf as bs cs, add_sub_cancel_left]

protected theorem comp_eq_of_monotoneOn {Î² : Type*} [LinearOrder Î²] (f : Î± â†’ E) {t : Set Î²}
    (Ï† : Î² â†’ Î±) (hÏ† : MonotoneOn Ï† t) {x y : Î²} (hx : x âˆˆ t) (hy : y âˆˆ t) :
    variationOnFromTo (f âˆ˜ Ï†) t x y = variationOnFromTo f (Ï† '' t) (Ï† x) (Ï† y) := by
  rcases le_total x y with (h | h)
  Â· rw [variationOnFromTo.eq_of_le _ _ h, variationOnFromTo.eq_of_le _ _ (hÏ† hx hy h),
      eVariationOn.comp_inter_Icc_eq_of_monotoneOn f Ï† hÏ† hx hy]
  Â· rw [variationOnFromTo.eq_of_ge _ _ h, variationOnFromTo.eq_of_ge _ _ (hÏ† hy hx h),
      eVariationOn.comp_inter_Icc_eq_of_monotoneOn f Ï† hÏ† hy hx]

end variationOnFromTo

/-- If a real-valued function has bounded variation on a set, then it is a difference of monotone
functions there. -/
theorem LocallyBoundedVariationOn.exists_monotoneOn_sub_monotoneOn {f : Î± â†’ â„} {s : Set Î±}
    (h : LocallyBoundedVariationOn f s) :
    âˆƒ p q : Î± â†’ â„, MonotoneOn p s âˆ§ MonotoneOn q s âˆ§ f = p - q := by
  rcases eq_empty_or_nonempty s with (rfl | âŸ¨c, csâŸ©)
  Â· exact âŸ¨f, 0, subsingleton_empty.monotoneOn _, subsingleton_empty.monotoneOn _,
      (sub_zero f).symmâŸ©
  Â· exact âŸ¨_, _, variationOnFromTo.monotoneOn h cs, variationOnFromTo.sub_self_monotoneOn h cs,
      (sub_sub_cancel _ _).symmâŸ©

/-! ## Lipschitz functions and bounded variation -/

section LipschitzOnWith

variable {F : Type*} [PseudoEMetricSpace F]

theorem LipschitzOnWith.comp_eVariationOn_le {f : E â†’ F} {C : â„â‰¥0} {t : Set E}
    (h : LipschitzOnWith C f t) {g : Î± â†’ E} {s : Set Î±} (hg : MapsTo g s t) :
    eVariationOn (f âˆ˜ g) s â‰¤ C * eVariationOn g s := by
  apply iSup_le _
  rintro âŸ¨n, âŸ¨u, hu, usâŸ©âŸ©
  calc
    (âˆ‘ i âˆˆ Finset.range n, edist (f (g (u (i + 1)))) (f (g (u i)))) â‰¤
        âˆ‘ i âˆˆ Finset.range n, C * edist (g (u (i + 1))) (g (u i)) :=
      Finset.sum_le_sum fun i _ => h (hg (us _)) (hg (us _))
    _ = C * âˆ‘ i âˆˆ Finset.range n, edist (g (u (i + 1))) (g (u i)) := by rw [Finset.mul_sum]
    _ â‰¤ C * eVariationOn g s := mul_le_mul_left' (eVariationOn.sum_le _ _ hu us) _

theorem LipschitzOnWith.comp_boundedVariationOn {f : E â†’ F} {C : â„â‰¥0} {t : Set E}
    (hf : LipschitzOnWith C f t) {g : Î± â†’ E} {s : Set Î±} (hg : MapsTo g s t)
    (h : BoundedVariationOn g s) : BoundedVariationOn (f âˆ˜ g) s :=
  ne_top_of_le_ne_top (by finiteness) (hf.comp_eVariationOn_le hg)

theorem LipschitzOnWith.comp_locallyBoundedVariationOn {f : E â†’ F} {C : â„â‰¥0} {t : Set E}
    (hf : LipschitzOnWith C f t) {g : Î± â†’ E} {s : Set Î±} (hg : MapsTo g s t)
    (h : LocallyBoundedVariationOn g s) : LocallyBoundedVariationOn (f âˆ˜ g) s :=
  fun x y xs ys =>
  hf.comp_boundedVariationOn (hg.mono_left inter_subset_left) (h x y xs ys)

theorem LipschitzWith.comp_boundedVariationOn {f : E â†’ F} {C : â„â‰¥0} (hf : LipschitzWith C f)
    {g : Î± â†’ E} {s : Set Î±} (h : BoundedVariationOn g s) : BoundedVariationOn (f âˆ˜ g) s :=
  hf.lipschitzOnWith.comp_boundedVariationOn (mapsTo_univ _ _) h

theorem LipschitzWith.comp_locallyBoundedVariationOn {f : E â†’ F} {C : â„â‰¥0}
    (hf : LipschitzWith C f) {g : Î± â†’ E} {s : Set Î±} (h : LocallyBoundedVariationOn g s) :
    LocallyBoundedVariationOn (f âˆ˜ g) s :=
  hf.lipschitzOnWith.comp_locallyBoundedVariationOn (mapsTo_univ _ _) h

theorem LipschitzOnWith.locallyBoundedVariationOn {f : â„ â†’ E} {C : â„â‰¥0} {s : Set â„}
    (hf : LipschitzOnWith C f s) : LocallyBoundedVariationOn f s :=
  hf.comp_locallyBoundedVariationOn (mapsTo_id _)
    (@monotoneOn_id â„ _ s).locallyBoundedVariationOn

theorem LipschitzWith.locallyBoundedVariationOn {f : â„ â†’ E} {C : â„â‰¥0} (hf : LipschitzWith C f)
    (s : Set â„) : LocallyBoundedVariationOn f s :=
  hf.lipschitzOnWith.locallyBoundedVariationOn

end LipschitzOnWith
