/-
Copyright (c) 2019 Reid Barton. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Patrick Massot, S√©bastien Gou√´zel, Zhouhang Zhou, Reid Barton
-/
import Mathlib.Logic.Equiv.Fin
import Mathlib.Topology.DenseEmbedding
import Mathlib.Topology.Support

#align_import topology.homeomorph from "leanprover-community/mathlib"@"4c3e1721c58ef9087bbc2c8c38b540f70eda2e53"

/-!
# Homeomorphisms

This file defines homeomorphisms between two topological spaces. They are bijections with both
directions continuous. We denote homeomorphisms with the notation `‚âÉ‚Çú`.

# Main definitions

* `Homeomorph Œ± Œ≤`: The type of homeomorphisms from `Œ±` to `Œ≤`.
  This type can be denoted using the following notation: `Œ± ‚âÉ‚Çú Œ≤`.

# Main results

* Pretty much every topological property is preserved under homeomorphisms.
* `Homeomorph.homeomorphOfContinuousOpen`: A continuous bijection that is
  an open map is a homeomorphism.

-/

open Set Filter

open Topology

variable {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*} {Œ¥ : Type*}

-- not all spaces are homeomorphic to each other
/-- Homeomorphism between `Œ±` and `Œ≤`, also called topological isomorphism -/
structure Homeomorph (Œ± : Type*) (Œ≤ : Type*) [TopologicalSpace Œ±] [TopologicalSpace Œ≤]
    extends Œ± ‚âÉ Œ≤ where
  /-- The forward map of a homeomorphism is a continuous function. -/
  continuous_toFun : Continuous toFun := by continuity
  /-- The inverse map of a homeomorphism is a continuous function. -/
  continuous_invFun : Continuous invFun := by continuity
#align homeomorph Homeomorph

@[inherit_doc]
infixl:25 " ‚âÉ‚Çú " => Homeomorph

namespace Homeomorph

variable [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [TopologicalSpace Œ≥] [TopologicalSpace Œ¥]

theorem toEquiv_injective : Function.Injective (toEquiv : Œ± ‚âÉ‚Çú Œ≤ ‚Üí Œ± ‚âÉ Œ≤)
  | ‚ü®_, _, _‚ü©, ‚ü®_, _, _‚ü©, rfl => rfl
#align homeomorph.to_equiv_injective Homeomorph.toEquiv_injective

instance : EquivLike (Œ± ‚âÉ‚Çú Œ≤) Œ± Œ≤ where
  coe := fun h => h.toEquiv
  inv := fun h => h.toEquiv.symm
  left_inv := fun h => h.left_inv
  right_inv := fun h => h.right_inv
  coe_injective' := fun _ _ H _ => toEquiv_injective <| FunLike.ext' H

instance : CoeFun (Œ± ‚âÉ‚Çú Œ≤) fun _ ‚Ü¶ Œ± ‚Üí Œ≤ := ‚ü®FunLike.coe‚ü©

@[simp]
theorem homeomorph_mk_coe (a : Equiv Œ± Œ≤) (b c) : (Homeomorph.mk a b c : Œ± ‚Üí Œ≤) = a :=
  rfl
#align homeomorph.homeomorph_mk_coe Homeomorph.homeomorph_mk_coe

/-- Inverse of a homeomorphism. -/
protected def symm (h : Œ± ‚âÉ‚Çú Œ≤) : Œ≤ ‚âÉ‚Çú Œ± where
  continuous_toFun := h.continuous_invFun
  continuous_invFun := h.continuous_toFun
  toEquiv := h.toEquiv.symm
#align homeomorph.symm Homeomorph.symm

@[simp] theorem symm_symm (h : Œ± ‚âÉ‚Çú Œ≤) : h.symm.symm = h := rfl
#align homeomorph.symm_symm Homeomorph.symm_symm

/-- See Note [custom simps projection] -/
def Simps.symm_apply (h : Œ± ‚âÉ‚Çú Œ≤) : Œ≤ ‚Üí Œ± :=
  h.symm
#align homeomorph.simps.symm_apply Homeomorph.Simps.symm_apply

initialize_simps_projections Homeomorph (toFun ‚Üí apply, invFun ‚Üí symm_apply)

@[simp]
theorem coe_toEquiv (h : Œ± ‚âÉ‚Çú Œ≤) : ‚áëh.toEquiv = h :=
  rfl
#align homeomorph.coe_to_equiv Homeomorph.coe_toEquiv

@[simp]
theorem coe_symm_toEquiv (h : Œ± ‚âÉ‚Çú Œ≤) : ‚áëh.toEquiv.symm = h.symm :=
  rfl
#align homeomorph.coe_symm_to_equiv Homeomorph.coe_symm_toEquiv

@[ext]
theorem ext {h h' : Œ± ‚âÉ‚Çú Œ≤} (H : ‚àÄ x, h x = h' x) : h = h' :=
  FunLike.ext _ _ H
#align homeomorph.ext Homeomorph.ext

/-- Identity map as a homeomorphism. -/
@[simps! (config := { fullyApplied := false }) apply]
protected def refl (Œ± : Type*) [TopologicalSpace Œ±] : Œ± ‚âÉ‚Çú Œ± where
  continuous_toFun := continuous_id
  continuous_invFun := continuous_id
  toEquiv := Equiv.refl Œ±
#align homeomorph.refl Homeomorph.refl

/-- Composition of two homeomorphisms. -/
protected def trans (h‚ÇÅ : Œ± ‚âÉ‚Çú Œ≤) (h‚ÇÇ : Œ≤ ‚âÉ‚Çú Œ≥) : Œ± ‚âÉ‚Çú Œ≥ where
  continuous_toFun := h‚ÇÇ.continuous_toFun.comp h‚ÇÅ.continuous_toFun
  continuous_invFun := h‚ÇÅ.continuous_invFun.comp h‚ÇÇ.continuous_invFun
  toEquiv := Equiv.trans h‚ÇÅ.toEquiv h‚ÇÇ.toEquiv
#align homeomorph.trans Homeomorph.trans

@[simp]
theorem trans_apply (h‚ÇÅ : Œ± ‚âÉ‚Çú Œ≤) (h‚ÇÇ : Œ≤ ‚âÉ‚Çú Œ≥) (a : Œ±) : h‚ÇÅ.trans h‚ÇÇ a = h‚ÇÇ (h‚ÇÅ a) :=
  rfl
#align homeomorph.trans_apply Homeomorph.trans_apply

@[simp] theorem symm_trans_apply (f : Œ± ‚âÉ‚Çú Œ≤) (g : Œ≤ ‚âÉ‚Çú Œ≥) (a : Œ≥) :
    (f.trans g).symm a = f.symm (g.symm a) := rfl

@[simp]
theorem homeomorph_mk_coe_symm (a : Equiv Œ± Œ≤) (b c) :
    ((Homeomorph.mk a b c).symm : Œ≤ ‚Üí Œ±) = a.symm :=
  rfl
#align homeomorph.homeomorph_mk_coe_symm Homeomorph.homeomorph_mk_coe_symm

@[simp]
theorem refl_symm : (Homeomorph.refl Œ±).symm = Homeomorph.refl Œ± :=
  rfl
#align homeomorph.refl_symm Homeomorph.refl_symm

@[continuity]
protected theorem continuous (h : Œ± ‚âÉ‚Çú Œ≤) : Continuous h :=
  h.continuous_toFun
#align homeomorph.continuous Homeomorph.continuous

-- otherwise `by continuity` can't prove continuity of `h.to_equiv.symm`
@[continuity]
protected theorem continuous_symm (h : Œ± ‚âÉ‚Çú Œ≤) : Continuous h.symm :=
  h.continuous_invFun
#align homeomorph.continuous_symm Homeomorph.continuous_symm

@[simp]
theorem apply_symm_apply (h : Œ± ‚âÉ‚Çú Œ≤) (x : Œ≤) : h (h.symm x) = x :=
  h.toEquiv.apply_symm_apply x
#align homeomorph.apply_symm_apply Homeomorph.apply_symm_apply

@[simp]
theorem symm_apply_apply (h : Œ± ‚âÉ‚Çú Œ≤) (x : Œ±) : h.symm (h x) = x :=
  h.toEquiv.symm_apply_apply x
#align homeomorph.symm_apply_apply Homeomorph.symm_apply_apply

@[simp]
theorem self_trans_symm (h : Œ± ‚âÉ‚Çú Œ≤) : h.trans h.symm = Homeomorph.refl Œ± := by
  ext
  apply symm_apply_apply
#align homeomorph.self_trans_symm Homeomorph.self_trans_symm

@[simp]
theorem symm_trans_self (h : Œ± ‚âÉ‚Çú Œ≤) : h.symm.trans h = Homeomorph.refl Œ≤ := by
  ext
  apply apply_symm_apply
#align homeomorph.symm_trans_self Homeomorph.symm_trans_self

protected theorem bijective (h : Œ± ‚âÉ‚Çú Œ≤) : Function.Bijective h :=
  h.toEquiv.bijective
#align homeomorph.bijective Homeomorph.bijective

protected theorem injective (h : Œ± ‚âÉ‚Çú Œ≤) : Function.Injective h :=
  h.toEquiv.injective
#align homeomorph.injective Homeomorph.injective

protected theorem surjective (h : Œ± ‚âÉ‚Çú Œ≤) : Function.Surjective h :=
  h.toEquiv.surjective
#align homeomorph.surjective Homeomorph.surjective

/-- Change the homeomorphism `f` to make the inverse function definitionally equal to `g`. -/
def changeInv (f : Œ± ‚âÉ‚Çú Œ≤) (g : Œ≤ ‚Üí Œ±) (hg : Function.RightInverse g f) : Œ± ‚âÉ‚Çú Œ≤ :=
  haveI : g = f.symm := (f.left_inv.eq_rightInverse hg).symm
  { toFun := f
    invFun := g
    left_inv := by convert f.left_inv
    right_inv := by convert f.right_inv using 1
    continuous_toFun := f.continuous
    continuous_invFun := by convert f.symm.continuous }
#align homeomorph.change_inv Homeomorph.changeInv

@[simp]
theorem symm_comp_self (h : Œ± ‚âÉ‚Çú Œ≤) : h.symm ‚àò h = id :=
  funext h.symm_apply_apply
#align homeomorph.symm_comp_self Homeomorph.symm_comp_self

@[simp]
theorem self_comp_symm (h : Œ± ‚âÉ‚Çú Œ≤) : h ‚àò h.symm = id :=
  funext h.apply_symm_apply
#align homeomorph.self_comp_symm Homeomorph.self_comp_symm

@[simp]
theorem range_coe (h : Œ± ‚âÉ‚Çú Œ≤) : range h = univ :=
  h.surjective.range_eq
#align homeomorph.range_coe Homeomorph.range_coe

theorem image_symm (h : Œ± ‚âÉ‚Çú Œ≤) : image h.symm = preimage h :=
  funext h.symm.toEquiv.image_eq_preimage
#align homeomorph.image_symm Homeomorph.image_symm

theorem preimage_symm (h : Œ± ‚âÉ‚Çú Œ≤) : preimage h.symm = image h :=
  (funext h.toEquiv.image_eq_preimage).symm
#align homeomorph.preimage_symm Homeomorph.preimage_symm

@[simp]
theorem image_preimage (h : Œ± ‚âÉ‚Çú Œ≤) (s : Set Œ≤) : h '' (h ‚Åª¬π' s) = s :=
  h.toEquiv.image_preimage s
#align homeomorph.image_preimage Homeomorph.image_preimage

@[simp]
theorem preimage_image (h : Œ± ‚âÉ‚Çú Œ≤) (s : Set Œ±) : h ‚Åª¬π' (h '' s) = s :=
  h.toEquiv.preimage_image s
#align homeomorph.preimage_image Homeomorph.preimage_image

protected theorem inducing (h : Œ± ‚âÉ‚Çú Œ≤) : Inducing h :=
  inducing_of_inducing_compose h.continuous h.symm.continuous <| by
    simp only [symm_comp_self, inducing_id]
#align homeomorph.inducing Homeomorph.inducing

theorem induced_eq (h : Œ± ‚âÉ‚Çú Œ≤) : TopologicalSpace.induced h ‚Äπ_‚Ä∫ = ‚Äπ_‚Ä∫ :=
  h.inducing.1.symm
#align homeomorph.induced_eq Homeomorph.induced_eq

protected theorem quotientMap (h : Œ± ‚âÉ‚Çú Œ≤) : QuotientMap h :=
  QuotientMap.of_quotientMap_compose h.symm.continuous h.continuous <| by
    simp only [self_comp_symm, QuotientMap.id]
#align homeomorph.quotient_map Homeomorph.quotientMap

theorem coinduced_eq (h : Œ± ‚âÉ‚Çú Œ≤) : TopologicalSpace.coinduced h ‚Äπ_‚Ä∫ = ‚Äπ_‚Ä∫ :=
  h.quotientMap.2.symm
#align homeomorph.coinduced_eq Homeomorph.coinduced_eq

protected theorem embedding (h : Œ± ‚âÉ‚Çú Œ≤) : Embedding h :=
  ‚ü®h.inducing, h.injective‚ü©
#align homeomorph.embedding Homeomorph.embedding

/-- Homeomorphism given an embedding. -/
noncomputable def ofEmbedding (f : Œ± ‚Üí Œ≤) (hf : Embedding f) : Œ± ‚âÉ‚Çú Set.range f where
  continuous_toFun := hf.continuous.subtype_mk _
  continuous_invFun := hf.continuous_iff.2 <| by simp [continuous_subtype_val]
  toEquiv := Equiv.ofInjective f hf.inj
#align homeomorph.of_embedding Homeomorph.ofEmbedding

protected theorem secondCountableTopology [TopologicalSpace.SecondCountableTopology Œ≤]
    (h : Œ± ‚âÉ‚Çú Œ≤) : TopologicalSpace.SecondCountableTopology Œ± :=
  h.inducing.secondCountableTopology
#align homeomorph.second_countable_topology Homeomorph.secondCountableTopology

theorem isCompact_image {s : Set Œ±} (h : Œ± ‚âÉ‚Çú Œ≤) : IsCompact (h '' s) ‚Üî IsCompact s :=
  h.embedding.isCompact_iff_isCompact_image.symm
#align homeomorph.is_compact_image Homeomorph.isCompact_image

theorem isCompact_preimage {s : Set Œ≤} (h : Œ± ‚âÉ‚Çú Œ≤) : IsCompact (h ‚Åª¬π' s) ‚Üî IsCompact s := by
  rw [‚Üê image_symm]; exact h.symm.isCompact_image
#align homeomorph.is_compact_preimage Homeomorph.isCompact_preimage

@[simp]
theorem isPreconnected_image {s : Set Œ±} (h : Œ± ‚âÉ‚Çú Œ≤) :
    IsPreconnected (h '' s) ‚Üî IsPreconnected s :=
  ‚ü®fun hs ‚Ü¶ by simpa only [image_symm, preimage_image]
    using hs.image _ h.symm.continuous.continuousOn,
    fun hs ‚Ü¶ hs.image _ h.continuous.continuousOn‚ü©

@[simp]
theorem isPreconnected_preimage {s : Set Œ≤} (h : Œ± ‚âÉ‚Çú Œ≤) :
    IsPreconnected (h ‚Åª¬π' s) ‚Üî IsPreconnected s := by
  rw [‚Üê image_symm, isPreconnected_image]

@[simp]
theorem isConnected_image {s : Set Œ±} (h : Œ± ‚âÉ‚Çú Œ≤) :
    IsConnected (h '' s) ‚Üî IsConnected s :=
  nonempty_image_iff.and h.isPreconnected_image

@[simp]
theorem isConnected_preimage {s : Set Œ≤} (h : Œ± ‚âÉ‚Çú Œ≤) :
    IsConnected (h ‚Åª¬π' s) ‚Üî IsConnected s := by
  rw [‚Üê image_symm, isConnected_image]

@[simp]
theorem comap_cocompact (h : Œ± ‚âÉ‚Çú Œ≤) : comap h (cocompact Œ≤) = cocompact Œ± :=
  (comap_cocompact_le h.continuous).antisymm <|
    (hasBasis_cocompact.le_basis_iff (hasBasis_cocompact.comap h)).2 fun K hK =>
      ‚ü®h ‚Åª¬π' K, h.isCompact_preimage.2 hK, Subset.rfl‚ü©
#align homeomorph.comap_cocompact Homeomorph.comap_cocompact

@[simp]
theorem map_cocompact (h : Œ± ‚âÉ‚Çú Œ≤) : map h (cocompact Œ±) = cocompact Œ≤ := by
  rw [‚Üê h.comap_cocompact, map_comap_of_surjective h.surjective]
#align homeomorph.map_cocompact Homeomorph.map_cocompact

protected theorem compactSpace [CompactSpace Œ±] (h : Œ± ‚âÉ‚Çú Œ≤) : CompactSpace Œ≤ where
  isCompact_univ := h.symm.isCompact_preimage.2 isCompact_univ
#align homeomorph.compact_space Homeomorph.compactSpace

protected theorem t0Space [T0Space Œ±] (h : Œ± ‚âÉ‚Çú Œ≤) : T0Space Œ≤ :=
  h.symm.embedding.t0Space
#align homeomorph.t0_space Homeomorph.t0Space

protected theorem t1Space [T1Space Œ±] (h : Œ± ‚âÉ‚Çú Œ≤) : T1Space Œ≤ :=
  h.symm.embedding.t1Space
#align homeomorph.t1_space Homeomorph.t1Space

protected theorem t2Space [T2Space Œ±] (h : Œ± ‚âÉ‚Çú Œ≤) : T2Space Œ≤ :=
  h.symm.embedding.t2Space
#align homeomorph.t2_space Homeomorph.t2Space

protected theorem t3Space [T3Space Œ±] (h : Œ± ‚âÉ‚Çú Œ≤) : T3Space Œ≤ :=
  h.symm.embedding.t3Space
#align homeomorph.t3_space Homeomorph.t3Space

protected theorem denseEmbedding (h : Œ± ‚âÉ‚Çú Œ≤) : DenseEmbedding h :=
  { h.embedding with dense := h.surjective.denseRange }
#align homeomorph.dense_embedding Homeomorph.denseEmbedding

@[simp]
theorem isOpen_preimage (h : Œ± ‚âÉ‚Çú Œ≤) {s : Set Œ≤} : IsOpen (h ‚Åª¬π' s) ‚Üî IsOpen s :=
  h.quotientMap.isOpen_preimage
#align homeomorph.is_open_preimage Homeomorph.isOpen_preimage

@[simp]
theorem isOpen_image (h : Œ± ‚âÉ‚Çú Œ≤) {s : Set Œ±} : IsOpen (h '' s) ‚Üî IsOpen s := by
  rw [‚Üê preimage_symm, isOpen_preimage]
#align homeomorph.is_open_image Homeomorph.isOpen_image

protected theorem isOpenMap (h : Œ± ‚âÉ‚Çú Œ≤) : IsOpenMap h := fun _ => h.isOpen_image.2
#align homeomorph.is_open_map Homeomorph.isOpenMap

@[simp]
theorem isClosed_preimage (h : Œ± ‚âÉ‚Çú Œ≤) {s : Set Œ≤} : IsClosed (h ‚Åª¬π' s) ‚Üî IsClosed s := by
  simp only [‚Üê isOpen_compl_iff, ‚Üê preimage_compl, isOpen_preimage]
#align homeomorph.is_closed_preimage Homeomorph.isClosed_preimage

@[simp]
theorem isClosed_image (h : Œ± ‚âÉ‚Çú Œ≤) {s : Set Œ±} : IsClosed (h '' s) ‚Üî IsClosed s := by
  rw [‚Üê preimage_symm, isClosed_preimage]
#align homeomorph.is_closed_image Homeomorph.isClosed_image

protected theorem isClosedMap (h : Œ± ‚âÉ‚Çú Œ≤) : IsClosedMap h := fun _ => h.isClosed_image.2
#align homeomorph.is_closed_map Homeomorph.isClosedMap

protected theorem openEmbedding (h : Œ± ‚âÉ‚Çú Œ≤) : OpenEmbedding h :=
  openEmbedding_of_embedding_open h.embedding h.isOpenMap
#align homeomorph.open_embedding Homeomorph.openEmbedding

protected theorem closedEmbedding (h : Œ± ‚âÉ‚Çú Œ≤) : ClosedEmbedding h :=
  closedEmbedding_of_embedding_closed h.embedding h.isClosedMap
#align homeomorph.closed_embedding Homeomorph.closedEmbedding

protected theorem normalSpace [NormalSpace Œ±] (h : Œ± ‚âÉ‚Çú Œ≤) : NormalSpace Œ≤ :=
  h.symm.closedEmbedding.normalSpace

protected theorem t4Space [T4Space Œ±] (h : Œ± ‚âÉ‚Çú Œ≤) : T4Space Œ≤ :=
  h.symm.closedEmbedding.t4Space
#align homeomorph.normal_space Homeomorph.t4Space

theorem preimage_closure (h : Œ± ‚âÉ‚Çú Œ≤) (s : Set Œ≤) : h ‚Åª¬π' closure s = closure (h ‚Åª¬π' s) :=
  h.isOpenMap.preimage_closure_eq_closure_preimage h.continuous _
#align homeomorph.preimage_closure Homeomorph.preimage_closure

theorem image_closure (h : Œ± ‚âÉ‚Çú Œ≤) (s : Set Œ±) : h '' closure s = closure (h '' s) := by
  rw [‚Üê preimage_symm, preimage_closure]
#align homeomorph.image_closure Homeomorph.image_closure

theorem preimage_interior (h : Œ± ‚âÉ‚Çú Œ≤) (s : Set Œ≤) : h ‚Åª¬π' interior s = interior (h ‚Åª¬π' s) :=
  h.isOpenMap.preimage_interior_eq_interior_preimage h.continuous _
#align homeomorph.preimage_interior Homeomorph.preimage_interior

theorem image_interior (h : Œ± ‚âÉ‚Çú Œ≤) (s : Set Œ±) : h '' interior s = interior (h '' s) := by
  rw [‚Üê preimage_symm, preimage_interior]
#align homeomorph.image_interior Homeomorph.image_interior

theorem preimage_frontier (h : Œ± ‚âÉ‚Çú Œ≤) (s : Set Œ≤) : h ‚Åª¬π' frontier s = frontier (h ‚Åª¬π' s) :=
  h.isOpenMap.preimage_frontier_eq_frontier_preimage h.continuous _
#align homeomorph.preimage_frontier Homeomorph.preimage_frontier

theorem image_frontier (h : Œ± ‚âÉ‚Çú Œ≤) (s : Set Œ±) : h '' frontier s = frontier (h '' s) := by
  rw [‚Üê preimage_symm, preimage_frontier]
#align homeomorph.image_frontier Homeomorph.image_frontier

@[to_additive]
theorem _root_.HasCompactMulSupport.comp_homeomorph {M} [One M] {f : Œ≤ ‚Üí M}
    (hf : HasCompactMulSupport f) (œÜ : Œ± ‚âÉ‚Çú Œ≤) : HasCompactMulSupport (f ‚àò œÜ) :=
  hf.comp_closedEmbedding œÜ.closedEmbedding
#align has_compact_mul_support.comp_homeomorph HasCompactMulSupport.comp_homeomorph
#align has_compact_support.comp_homeomorph HasCompactSupport.comp_homeomorph

@[simp]
theorem map_nhds_eq (h : Œ± ‚âÉ‚Çú Œ≤) (x : Œ±) : map h (ùìù x) = ùìù (h x) :=
  h.embedding.map_nhds_of_mem _ (by simp)
#align homeomorph.map_nhds_eq Homeomorph.map_nhds_eq

theorem symm_map_nhds_eq (h : Œ± ‚âÉ‚Çú Œ≤) (x : Œ±) : map h.symm (ùìù (h x)) = ùìù x := by
  rw [h.symm.map_nhds_eq, h.symm_apply_apply]
#align homeomorph.symm_map_nhds_eq Homeomorph.symm_map_nhds_eq

theorem nhds_eq_comap (h : Œ± ‚âÉ‚Çú Œ≤) (x : Œ±) : ùìù x = comap h (ùìù (h x)) :=
  h.inducing.nhds_eq_comap x
#align homeomorph.nhds_eq_comap Homeomorph.nhds_eq_comap

@[simp]
theorem comap_nhds_eq (h : Œ± ‚âÉ‚Çú Œ≤) (y : Œ≤) : comap h (ùìù y) = ùìù (h.symm y) := by
  rw [h.nhds_eq_comap, h.apply_symm_apply]
#align homeomorph.comap_nhds_eq Homeomorph.comap_nhds_eq

/-- If the codomain of a homeomorphism is a locally connected space, then the domain is also
a locally connected space. -/
theorem locallyConnectedSpace [i : LocallyConnectedSpace Œ≤] (h : Œ± ‚âÉ‚Çú Œ≤) :
    LocallyConnectedSpace Œ± := by
  have : ‚àÄ x, (ùìù x).HasBasis (fun s ‚Ü¶ IsOpen s ‚àß h x ‚àà s ‚àß IsConnected s)
      (h.symm '' ¬∑) := fun x ‚Ü¶ by
    rw [‚Üê h.symm_map_nhds_eq]
    exact (i.1 _).map _
  refine locallyConnectedSpace_of_connected_bases _ _ this fun _ _ hs ‚Ü¶ ?_
  exact hs.2.2.2.image _ h.symm.continuous.continuousOn

/-- If a bijective map `e : Œ± ‚âÉ Œ≤` is continuous and open, then it is a homeomorphism. -/
def homeomorphOfContinuousOpen (e : Œ± ‚âÉ Œ≤) (h‚ÇÅ : Continuous e) (h‚ÇÇ : IsOpenMap e) : Œ± ‚âÉ‚Çú Œ≤ where
  continuous_toFun := h‚ÇÅ
  continuous_invFun := by
    rw [continuous_def]
    intro s hs
    convert ‚Üê h‚ÇÇ s hs using 1
    apply e.image_eq_preimage
  toEquiv := e
#align homeomorph.homeomorph_of_continuous_open Homeomorph.homeomorphOfContinuousOpen

@[simp]
theorem comp_continuousOn_iff (h : Œ± ‚âÉ‚Çú Œ≤) (f : Œ≥ ‚Üí Œ±) (s : Set Œ≥) :
    ContinuousOn (h ‚àò f) s ‚Üî ContinuousOn f s :=
  h.inducing.continuousOn_iff.symm
#align homeomorph.comp_continuous_on_iff Homeomorph.comp_continuousOn_iff

@[simp]
theorem comp_continuous_iff (h : Œ± ‚âÉ‚Çú Œ≤) {f : Œ≥ ‚Üí Œ±} : Continuous (h ‚àò f) ‚Üî Continuous f :=
  h.inducing.continuous_iff.symm
#align homeomorph.comp_continuous_iff Homeomorph.comp_continuous_iff

@[simp]
theorem comp_continuous_iff' (h : Œ± ‚âÉ‚Çú Œ≤) {f : Œ≤ ‚Üí Œ≥} : Continuous (f ‚àò h) ‚Üî Continuous f :=
  h.quotientMap.continuous_iff.symm
#align homeomorph.comp_continuous_iff' Homeomorph.comp_continuous_iff'

theorem comp_continuousAt_iff (h : Œ± ‚âÉ‚Çú Œ≤) (f : Œ≥ ‚Üí Œ±) (x : Œ≥) :
    ContinuousAt (h ‚àò f) x ‚Üî ContinuousAt f x :=
  h.inducing.continuousAt_iff.symm
#align homeomorph.comp_continuous_at_iff Homeomorph.comp_continuousAt_iff

theorem comp_continuousAt_iff' (h : Œ± ‚âÉ‚Çú Œ≤) (f : Œ≤ ‚Üí Œ≥) (x : Œ±) :
    ContinuousAt (f ‚àò h) x ‚Üî ContinuousAt f (h x) :=
  h.inducing.continuousAt_iff' (by simp)
#align homeomorph.comp_continuous_at_iff' Homeomorph.comp_continuousAt_iff'

theorem comp_continuousWithinAt_iff (h : Œ± ‚âÉ‚Çú Œ≤) (f : Œ≥ ‚Üí Œ±) (s : Set Œ≥) (x : Œ≥) :
    ContinuousWithinAt f s x ‚Üî ContinuousWithinAt (h ‚àò f) s x :=
  h.inducing.continuousWithinAt_iff
#align homeomorph.comp_continuous_within_at_iff Homeomorph.comp_continuousWithinAt_iff

@[simp]
theorem comp_isOpenMap_iff (h : Œ± ‚âÉ‚Çú Œ≤) {f : Œ≥ ‚Üí Œ±} : IsOpenMap (h ‚àò f) ‚Üî IsOpenMap f := by
  refine' ‚ü®_, fun hf => h.isOpenMap.comp hf‚ü©
  intro hf
  rw [‚Üê Function.comp.left_id f, ‚Üê h.symm_comp_self, Function.comp.assoc]
  exact h.symm.isOpenMap.comp hf
#align homeomorph.comp_is_open_map_iff Homeomorph.comp_isOpenMap_iff

@[simp]
theorem comp_isOpenMap_iff' (h : Œ± ‚âÉ‚Çú Œ≤) {f : Œ≤ ‚Üí Œ≥} : IsOpenMap (f ‚àò h) ‚Üî IsOpenMap f := by
  refine' ‚ü®_, fun hf => hf.comp h.isOpenMap‚ü©
  intro hf
  rw [‚Üê Function.comp.right_id f, ‚Üê h.self_comp_symm, ‚Üê Function.comp.assoc]
  exact hf.comp h.symm.isOpenMap
#align homeomorph.comp_is_open_map_iff' Homeomorph.comp_isOpenMap_iff'

/-- If two sets are equal, then they are homeomorphic. -/
def setCongr {s t : Set Œ±} (h : s = t) : s ‚âÉ‚Çú t where
  continuous_toFun := continuous_inclusion h.subset
  continuous_invFun := continuous_inclusion h.symm.subset
  toEquiv := Equiv.setCongr h
#align homeomorph.set_congr Homeomorph.setCongr

/-- Sum of two homeomorphisms. -/
def sumCongr (h‚ÇÅ : Œ± ‚âÉ‚Çú Œ≤) (h‚ÇÇ : Œ≥ ‚âÉ‚Çú Œ¥) : Sum Œ± Œ≥ ‚âÉ‚Çú Sum Œ≤ Œ¥ where
  continuous_toFun := h‚ÇÅ.continuous.sum_map h‚ÇÇ.continuous
  continuous_invFun := h‚ÇÅ.symm.continuous.sum_map h‚ÇÇ.symm.continuous
  toEquiv := h‚ÇÅ.toEquiv.sumCongr h‚ÇÇ.toEquiv
#align homeomorph.sum_congr Homeomorph.sumCongr

/-- Product of two homeomorphisms. -/
def prodCongr (h‚ÇÅ : Œ± ‚âÉ‚Çú Œ≤) (h‚ÇÇ : Œ≥ ‚âÉ‚Çú Œ¥) : Œ± √ó Œ≥ ‚âÉ‚Çú Œ≤ √ó Œ¥ where
  continuous_toFun := h‚ÇÅ.continuous.prod_map h‚ÇÇ.continuous
  continuous_invFun := h‚ÇÅ.symm.continuous.prod_map h‚ÇÇ.symm.continuous
  toEquiv := h‚ÇÅ.toEquiv.prodCongr h‚ÇÇ.toEquiv
#align homeomorph.prod_congr Homeomorph.prodCongr

@[simp]
theorem prodCongr_symm (h‚ÇÅ : Œ± ‚âÉ‚Çú Œ≤) (h‚ÇÇ : Œ≥ ‚âÉ‚Çú Œ¥) :
    (h‚ÇÅ.prodCongr h‚ÇÇ).symm = h‚ÇÅ.symm.prodCongr h‚ÇÇ.symm :=
  rfl
#align homeomorph.prod_congr_symm Homeomorph.prodCongr_symm

@[simp]
theorem coe_prodCongr (h‚ÇÅ : Œ± ‚âÉ‚Çú Œ≤) (h‚ÇÇ : Œ≥ ‚âÉ‚Çú Œ¥) : ‚áë(h‚ÇÅ.prodCongr h‚ÇÇ) = Prod.map h‚ÇÅ h‚ÇÇ :=
  rfl
#align homeomorph.coe_prod_congr Homeomorph.coe_prodCongr

section

variable (Œ± Œ≤ Œ≥)

/-- `Œ± √ó Œ≤` is homeomorphic to `Œ≤ √ó Œ±`. -/
def prodComm : Œ± √ó Œ≤ ‚âÉ‚Çú Œ≤ √ó Œ± where
  continuous_toFun := continuous_snd.prod_mk continuous_fst
  continuous_invFun := continuous_snd.prod_mk continuous_fst
  toEquiv := Equiv.prodComm Œ± Œ≤
#align homeomorph.prod_comm Homeomorph.prodComm

@[simp]
theorem prodComm_symm : (prodComm Œ± Œ≤).symm = prodComm Œ≤ Œ± :=
  rfl
#align homeomorph.prod_comm_symm Homeomorph.prodComm_symm

@[simp]
theorem coe_prodComm : ‚áë(prodComm Œ± Œ≤) = Prod.swap :=
  rfl
#align homeomorph.coe_prod_comm Homeomorph.coe_prodComm

/-- `(Œ± √ó Œ≤) √ó Œ≥` is homeomorphic to `Œ± √ó (Œ≤ √ó Œ≥)`. -/
def prodAssoc : (Œ± √ó Œ≤) √ó Œ≥ ‚âÉ‚Çú Œ± √ó Œ≤ √ó Œ≥ where
  continuous_toFun := continuous_fst.fst.prod_mk (continuous_fst.snd.prod_mk continuous_snd)
  continuous_invFun := (continuous_fst.prod_mk continuous_snd.fst).prod_mk continuous_snd.snd
  toEquiv := Equiv.prodAssoc Œ± Œ≤ Œ≥
#align homeomorph.prod_assoc Homeomorph.prodAssoc

/-- `Œ± √ó {*}` is homeomorphic to `Œ±`. -/
@[simps! (config := { fullyApplied := false }) apply]
def prodPUnit : Œ± √ó PUnit ‚âÉ‚Çú Œ± where
  toEquiv := Equiv.prodPUnit Œ±
  continuous_toFun := continuous_fst
  continuous_invFun := continuous_id.prod_mk continuous_const
#align homeomorph.prod_punit Homeomorph.prodPUnit

/-- `{*} √ó Œ±` is homeomorphic to `Œ±`. -/
def punitProd : PUnit √ó Œ± ‚âÉ‚Çú Œ± :=
  (prodComm _ _).trans (prodPUnit _)
#align homeomorph.punit_prod Homeomorph.punitProd

@[simp] theorem coe_punitProd : ‚áë(punitProd Œ±) = Prod.snd := rfl
#align homeomorph.coe_punit_prod Homeomorph.coe_punitProd

/-- If both `Œ±` and `Œ≤` have a unique element, then `Œ± ‚âÉ‚Çú Œ≤`. -/
@[simps!]
def homeomorphOfUnique [Unique Œ±] [Unique Œ≤] : Œ± ‚âÉ‚Çú Œ≤ :=
  { Equiv.equivOfUnique Œ± Œ≤ with
    continuous_toFun := continuous_const
    continuous_invFun := continuous_const }
#align homeomorph.homeomorph_of_unique Homeomorph.homeomorphOfUnique

end

/-- `Equiv.piCongrLeft` as a homeomorphism: this is the natural homeomorphism
`Œ† i, Œ≤ (e i) ‚âÉ‚Çú Œ† j, Œ≤ j` obtained from a bijection `Œπ ‚âÉ Œπ'`. -/
@[simps! apply toEquiv]
def piCongrLeft {Œπ Œπ' : Type*} {Œ≤ : Œπ' ‚Üí Type*} [‚àÄ j, TopologicalSpace (Œ≤ j)]
    (e : Œπ ‚âÉ Œπ') : (‚àÄ i, Œ≤ (e i)) ‚âÉ‚Çú ‚àÄ j, Œ≤ j where
  continuous_toFun := continuous_pi <| e.forall_congr_left.mp <| fun i ‚Ü¶ by
    simpa only [Equiv.toFun_as_coe_apply, Equiv.piCongrLeft_apply_apply] using continuous_apply i
  continuous_invFun := Pi.continuous_precomp' e
  toEquiv := Equiv.piCongrLeft _ e

/-- `Equiv.piCongrRight` as a homeomorphism: this is the natural homeomorphism
`Œ† i, Œ≤‚ÇÅ i ‚âÉ‚Çú Œ† j, Œ≤‚ÇÇ i` obtained from homeomorphisms `Œ≤‚ÇÅ i ‚âÉ‚Çú Œ≤‚ÇÇ i` for each `i`. -/
@[simps! apply toEquiv]
def piCongrRight {Œπ : Type*} {Œ≤‚ÇÅ Œ≤‚ÇÇ : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (Œ≤‚ÇÅ i)]
    [‚àÄ i, TopologicalSpace (Œ≤‚ÇÇ i)] (F : ‚àÄ i, Œ≤‚ÇÅ i ‚âÉ‚Çú Œ≤‚ÇÇ i) : (‚àÄ i, Œ≤‚ÇÅ i) ‚âÉ‚Çú ‚àÄ i, Œ≤‚ÇÇ i where
  continuous_toFun := Pi.continuous_postcomp' fun i ‚Ü¶ (F i).continuous
  continuous_invFun := Pi.continuous_postcomp' fun i ‚Ü¶ (F i).symm.continuous
  toEquiv := Equiv.piCongrRight fun i => (F i).toEquiv
#align homeomorph.Pi_congr_right Homeomorph.piCongrRight

@[simp]
theorem piCongrRight_symm {Œπ : Type*} {Œ≤‚ÇÅ Œ≤‚ÇÇ : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (Œ≤‚ÇÅ i)]
    [‚àÄ i, TopologicalSpace (Œ≤‚ÇÇ i)] (F : ‚àÄ i, Œ≤‚ÇÅ i ‚âÉ‚Çú Œ≤‚ÇÇ i) :
    (piCongrRight F).symm = piCongrRight fun i => (F i).symm :=
  rfl
#align homeomorph.Pi_congr_right_symm Homeomorph.piCongrRight_symm

/-- `Equiv.piCongr` as a homeomorphism: this is the natural homeomorphism
`Œ† i‚ÇÅ, Œ≤‚ÇÅ i ‚âÉ‚Çú Œ† i‚ÇÇ, Œ≤‚ÇÇ i‚ÇÇ` obtained from a bijection `Œπ‚ÇÅ ‚âÉ Œπ‚ÇÇ` and homeomorphisms
`Œ≤‚ÇÅ i‚ÇÅ ‚âÉ‚Çú Œ≤‚ÇÇ (e i‚ÇÅ)` for each `i‚ÇÅ : Œπ‚ÇÅ`. -/
@[simps! apply toEquiv]
def piCongr {Œπ‚ÇÅ Œπ‚ÇÇ : Type*} {Œ≤‚ÇÅ : Œπ‚ÇÅ ‚Üí Type*} {Œ≤‚ÇÇ : Œπ‚ÇÇ ‚Üí Type*}
    [‚àÄ i‚ÇÅ, TopologicalSpace (Œ≤‚ÇÅ i‚ÇÅ)] [‚àÄ i‚ÇÇ, TopologicalSpace (Œ≤‚ÇÇ i‚ÇÇ)]
    (e : Œπ‚ÇÅ ‚âÉ Œπ‚ÇÇ) (F : ‚àÄ i‚ÇÅ, Œ≤‚ÇÅ i‚ÇÅ ‚âÉ‚Çú Œ≤‚ÇÇ (e i‚ÇÅ)) : (‚àÄ i‚ÇÅ, Œ≤‚ÇÅ i‚ÇÅ) ‚âÉ‚Çú ‚àÄ i‚ÇÇ, Œ≤‚ÇÇ i‚ÇÇ :=
  (Homeomorph.piCongrRight F).trans (Homeomorph.piCongrLeft e)

-- porting note: TODO: align the order of universes with `Equiv.ulift`
/-- `ULift Œ±` is homeomorphic to `Œ±`. -/
def ulift.{u, v} {Œ± : Type u} [TopologicalSpace Œ±] : ULift.{v, u} Œ± ‚âÉ‚Çú Œ± where
  continuous_toFun := continuous_uLift_down
  continuous_invFun := continuous_uLift_up
  toEquiv := Equiv.ulift
#align homeomorph.ulift Homeomorph.ulift

section Distrib

/-- `(Œ± ‚äï Œ≤) √ó Œ≥` is homeomorphic to `Œ± √ó Œ≥ ‚äï Œ≤ √ó Œ≥`. -/
def sumProdDistrib : Sum Œ± Œ≤ √ó Œ≥ ‚âÉ‚Çú Sum (Œ± √ó Œ≥) (Œ≤ √ó Œ≥) :=
  Homeomorph.symm <|
    homeomorphOfContinuousOpen (Equiv.sumProdDistrib Œ± Œ≤ Œ≥).symm
        ((continuous_inl.prod_map continuous_id).sum_elim
          (continuous_inr.prod_map continuous_id)) <|
      (isOpenMap_inl.prod IsOpenMap.id).sum_elim (isOpenMap_inr.prod IsOpenMap.id)
#align homeomorph.sum_prod_distrib Homeomorph.sumProdDistrib

/-- `Œ± √ó (Œ≤ ‚äï Œ≥)` is homeomorphic to `Œ± √ó Œ≤ ‚äï Œ± √ó Œ≥`. -/
def prodSumDistrib : Œ± √ó Sum Œ≤ Œ≥ ‚âÉ‚Çú Sum (Œ± √ó Œ≤) (Œ± √ó Œ≥) :=
  (prodComm _ _).trans <| sumProdDistrib.trans <| sumCongr (prodComm _ _) (prodComm _ _)
#align homeomorph.prod_sum_distrib Homeomorph.prodSumDistrib

variable {Œπ : Type*} {œÉ : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (œÉ i)]

/-- `(Œ£ i, œÉ i) √ó Œ≤` is homeomorphic to `Œ£ i, (œÉ i √ó Œ≤)`. -/
def sigmaProdDistrib : (Œ£i, œÉ i) √ó Œ≤ ‚âÉ‚Çú Œ£i, œÉ i √ó Œ≤ :=
  Homeomorph.symm <|
    homeomorphOfContinuousOpen (Equiv.sigmaProdDistrib œÉ Œ≤).symm
      (continuous_sigma fun _ => continuous_sigmaMk.fst'.prod_mk continuous_snd)
      (isOpenMap_sigma.2 fun _ => isOpenMap_sigmaMk.prod IsOpenMap.id)
#align homeomorph.sigma_prod_distrib Homeomorph.sigmaProdDistrib

end Distrib

/-- If `Œπ` has a unique element, then `Œπ ‚Üí Œ±` is homeomorphic to `Œ±`. -/
@[simps! (config := { fullyApplied := false })]
def funUnique (Œπ Œ± : Type*) [Unique Œπ] [TopologicalSpace Œ±] : (Œπ ‚Üí Œ±) ‚âÉ‚Çú Œ± where
  toEquiv := Equiv.funUnique Œπ Œ±
  continuous_toFun := continuous_apply _
  continuous_invFun := continuous_pi fun _ => continuous_id
#align homeomorph.fun_unique Homeomorph.funUnique

/-- Homeomorphism between dependent functions `Œ† i : Fin 2, Œ± i` and `Œ± 0 √ó Œ± 1`. -/
@[simps! (config := { fullyApplied := false })]
def piFinTwo.{u} (Œ± : Fin 2 ‚Üí Type u) [‚àÄ i, TopologicalSpace (Œ± i)] : (‚àÄ i, Œ± i) ‚âÉ‚Çú Œ± 0 √ó Œ± 1 where
  toEquiv := piFinTwoEquiv Œ±
  continuous_toFun := (continuous_apply 0).prod_mk (continuous_apply 1)
  continuous_invFun := continuous_pi <| Fin.forall_fin_two.2 ‚ü®continuous_fst, continuous_snd‚ü©
#align homeomorph.pi_fin_two Homeomorph.piFinTwo

/-- Homeomorphism between `Œ±¬≤ = Fin 2 ‚Üí Œ±` and `Œ± √ó Œ±`. -/
@[simps! (config := { fullyApplied := false })]
def finTwoArrow : (Fin 2 ‚Üí Œ±) ‚âÉ‚Çú Œ± √ó Œ± :=
  { piFinTwo fun _ => Œ± with toEquiv := finTwoArrowEquiv Œ± }
#align homeomorph.fin_two_arrow Homeomorph.finTwoArrow

/-- A subset of a topological space is homeomorphic to its image under a homeomorphism.
-/
@[simps!]
def image (e : Œ± ‚âÉ‚Çú Œ≤) (s : Set Œ±) : s ‚âÉ‚Çú e '' s where
  -- porting note: todo: by continuity!
  continuous_toFun := e.continuous.continuousOn.restrict_mapsTo (mapsTo_image _ _)
  continuous_invFun := (e.symm.continuous.comp continuous_subtype_val).codRestrict _
  toEquiv := e.toEquiv.image s
#align homeomorph.image Homeomorph.image

/-- `Set.univ Œ±` is homeomorphic to `Œ±`. -/
@[simps! (config := { fullyApplied := false })]
def Set.univ (Œ± : Type*) [TopologicalSpace Œ±] : (univ : Set Œ±) ‚âÉ‚Çú Œ± where
  toEquiv := Equiv.Set.univ Œ±
  continuous_toFun := continuous_subtype_val
  continuous_invFun := continuous_id.subtype_mk _
#align homeomorph.set.univ Homeomorph.Set.univ

/-- `s √óÀ¢ t` is homeomorphic to `s √ó t`. -/
@[simps!]
def Set.prod (s : Set Œ±) (t : Set Œ≤) : ‚Ü•(s √óÀ¢ t) ‚âÉ‚Çú s √ó t where
  toEquiv := Equiv.Set.prod s t
  continuous_toFun :=
    (continuous_subtype_val.fst.subtype_mk _).prod_mk (continuous_subtype_val.snd.subtype_mk _)
  continuous_invFun :=
    (continuous_subtype_val.fst'.prod_mk continuous_subtype_val.snd').subtype_mk _
#align homeomorph.set.prod Homeomorph.Set.prod

section

variable {Œπ : Type*}

/-- The topological space `Œ† i, Œ≤ i` can be split as a product by separating the indices in Œπ
  depending on whether they satisfy a predicate p or not.-/
@[simps!]
def piEquivPiSubtypeProd (p : Œπ ‚Üí Prop) (Œ≤ : Œπ ‚Üí Type*) [‚àÄ i, TopologicalSpace (Œ≤ i)]
    [DecidablePred p] : (‚àÄ i, Œ≤ i) ‚âÉ‚Çú (‚àÄ i : { x // p x }, Œ≤ i) √ó ‚àÄ i : { x // ¬¨p x }, Œ≤ i
    where
  toEquiv := Equiv.piEquivPiSubtypeProd p Œ≤
  continuous_toFun := by
    apply Continuous.prod_mk <;> exact continuous_pi fun j => continuous_apply j.1
  continuous_invFun :=
    continuous_pi fun j => by
      dsimp only [Equiv.piEquivPiSubtypeProd]; split_ifs
      exacts [(continuous_apply _).comp continuous_fst, (continuous_apply _).comp continuous_snd]
#align homeomorph.pi_equiv_pi_subtype_prod Homeomorph.piEquivPiSubtypeProd

variable [DecidableEq Œπ] (i : Œπ)

/-- A product of topological spaces can be split as the binary product of one of the spaces and
  the product of all the remaining spaces. -/
@[simps!]
def piSplitAt (Œ≤ : Œπ ‚Üí Type*) [‚àÄ j, TopologicalSpace (Œ≤ j)] :
    (‚àÄ j, Œ≤ j) ‚âÉ‚Çú Œ≤ i √ó ‚àÄ j : { j // j ‚â† i }, Œ≤ j
    where
  toEquiv := Equiv.piSplitAt i Œ≤
  continuous_toFun := (continuous_apply i).prod_mk (continuous_pi fun j => continuous_apply j.1)
  continuous_invFun :=
    continuous_pi fun j => by
      dsimp only [Equiv.piSplitAt]
      split_ifs with h
      subst h
      exacts [continuous_fst, (continuous_apply _).comp continuous_snd]
#align homeomorph.pi_split_at Homeomorph.piSplitAt

variable (Œ≤)

/-- A product of copies of a topological space can be split as the binary product of one copy and
  the product of all the remaining copies. -/
@[simps!]
def funSplitAt : (Œπ ‚Üí Œ≤) ‚âÉ‚Çú Œ≤ √ó ({ j // j ‚â† i } ‚Üí Œ≤) :=
  piSplitAt i _
#align homeomorph.fun_split_at Homeomorph.funSplitAt

end

end Homeomorph

namespace Equiv
variable {Œ± Œ≤ Œ≥ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [TopologicalSpace Œ≥]

/-- An equiv between topological spaces respecting openness is a homeomorphism. -/
@[simps toEquiv]
def toHomeomorph (e : Œ± ‚âÉ Œ≤) (he : ‚àÄ s, IsOpen (e ‚Åª¬π' s) ‚Üî IsOpen s) : Œ± ‚âÉ‚Çú Œ≤ where
  toEquiv := e
  continuous_toFun := continuous_def.2 Œª s ‚Ü¶ (he _).2
  continuous_invFun := continuous_def.2 Œª s ‚Ü¶ by convert (he _).1; simp

@[simp] lemma coe_toHomeomorph (e : Œ± ‚âÉ Œ≤) (he) : ‚áë(e.toHomeomorph he) = e := rfl
lemma toHomeomorph_apply (e : Œ± ‚âÉ Œ≤) (he) (a : Œ±) : e.toHomeomorph he a = e a := rfl

@[simp] lemma toHomeomorph_refl :
  (Equiv.refl Œ±).toHomeomorph (Œª _s ‚Ü¶ Iff.rfl) = Homeomorph.refl _ := rfl

@[simp] lemma toHomeomorph_symm (e : Œ± ‚âÉ Œ≤) (he) :
  (e.toHomeomorph he).symm = e.symm.toHomeomorph Œª s ‚Ü¶ by convert (he _).symm; simp := rfl

lemma toHomeomorph_trans (e : Œ± ‚âÉ Œ≤) (f : Œ≤ ‚âÉ Œ≥) (he hf) :
    (e.trans f).toHomeomorph (Œª _s ‚Ü¶ (he _).trans (hf _)) =
    (e.toHomeomorph he).trans (f.toHomeomorph hf) := rfl

/-- An inducing equiv between topological spaces is a homeomorphism. -/
@[simps toEquiv] -- porting note: TODO: was `@[simps]`
def toHomeomorphOfInducing (f : Œ± ‚âÉ Œ≤) (hf : Inducing f) : Œ± ‚âÉ‚Çú Œ≤ :=
  { f with
    continuous_toFun := hf.continuous
    continuous_invFun := hf.continuous_iff.2 <| by simpa using continuous_id }
#align equiv.to_homeomorph_of_inducing Equiv.toHomeomorphOfInducing

end Equiv

namespace Continuous

variable [TopologicalSpace Œ±] [TopologicalSpace Œ≤]

theorem continuous_symm_of_equiv_compact_to_t2 [CompactSpace Œ±] [T2Space Œ≤] {f : Œ± ‚âÉ Œ≤}
    (hf : Continuous f) : Continuous f.symm := by
  rw [continuous_iff_isClosed]
  intro C hC
  have hC' : IsClosed (f '' C) := (hC.isCompact.image hf).isClosed
  rwa [Equiv.image_eq_preimage] at hC'
#align continuous.continuous_symm_of_equiv_compact_to_t2 Continuous.continuous_symm_of_equiv_compact_to_t2

/-- Continuous equivalences from a compact space to a T2 space are homeomorphisms.

This is not true when T2 is weakened to T1
(see `Continuous.homeoOfEquivCompactToT2.t1_counterexample`). -/
@[simps toEquiv] -- porting note: was `@[simps]`
def homeoOfEquivCompactToT2 [CompactSpace Œ±] [T2Space Œ≤] {f : Œ± ‚âÉ Œ≤} (hf : Continuous f) : Œ± ‚âÉ‚Çú Œ≤ :=
  { f with
    continuous_toFun := hf
    continuous_invFun := hf.continuous_symm_of_equiv_compact_to_t2 }
#align continuous.homeo_of_equiv_compact_to_t2 Continuous.homeoOfEquivCompactToT2

end Continuous
