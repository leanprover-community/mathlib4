/-
Copyright (c) 2019 Reid Barton. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Patrick Massot, SÃ©bastien GouÃ«zel, Zhouhang Zhou, Reid Barton
-/
import Mathlib.Logic.Equiv.Fin
import Mathlib.Topology.DenseEmbedding
import Mathlib.Topology.Support

#align_import topology.homeomorph from "leanprover-community/mathlib"@"4c3e1721c58ef9087bbc2c8c38b540f70eda2e53"

/-!
# Homeomorphisms

This file defines homeomorphisms between two topological spaces. They are bijections with both
directions continuous. We denote homeomorphisms with the notation `â‰ƒâ‚œ`.

# Main definitions

* `Homeomorph Î± Î²`: The type of homeomorphisms from `Î±` to `Î²`.
  This type can be denoted using the following notation: `Î± â‰ƒâ‚œ Î²`.

# Main results

* Pretty much every topological property is preserved under homeomorphisms.
* `Homeomorph.homeomorphOfContinuousOpen`: A continuous bijection that is
  an open map is a homeomorphism.

-/

open Set Filter

open Topology

variable {Î± : Type*} {Î² : Type*} {Î³ : Type*} {Î´ : Type*}

-- not all spaces are homeomorphic to each other
/-- Homeomorphism between `Î±` and `Î²`, also called topological isomorphism -/
structure Homeomorph (Î± : Type*) (Î² : Type*) [TopologicalSpace Î±] [TopologicalSpace Î²]
    extends Î± â‰ƒ Î² where
  /-- The forward map of a homeomorphism is a continuous function. -/
  continuous_toFun : Continuous toFun := by continuity
  /-- The inverse map of a homeomorphism is a continuous function. -/
  continuous_invFun : Continuous invFun := by continuity
#align homeomorph Homeomorph

@[inherit_doc]
infixl:25 " â‰ƒâ‚œ " => Homeomorph

namespace Homeomorph

variable [TopologicalSpace Î±] [TopologicalSpace Î²] [TopologicalSpace Î³] [TopologicalSpace Î´]

theorem toEquiv_injective : Function.Injective (toEquiv : Î± â‰ƒâ‚œ Î² â†’ Î± â‰ƒ Î²)
  | âŸ¨_, _, _âŸ©, âŸ¨_, _, _âŸ©, rfl => rfl
#align homeomorph.to_equiv_injective Homeomorph.toEquiv_injective

instance : EquivLike (Î± â‰ƒâ‚œ Î²) Î± Î² where
  coe := fun h => h.toEquiv
  inv := fun h => h.toEquiv.symm
  left_inv := fun h => h.left_inv
  right_inv := fun h => h.right_inv
  coe_injective' := fun _ _ H _ => toEquiv_injective <| FunLike.ext' H

instance : CoeFun (Î± â‰ƒâ‚œ Î²) fun _ â†¦ Î± â†’ Î² := âŸ¨FunLike.coeâŸ©

@[simp]
theorem homeomorph_mk_coe (a : Equiv Î± Î²) (b c) : (Homeomorph.mk a b c : Î± â†’ Î²) = a :=
  rfl
#align homeomorph.homeomorph_mk_coe Homeomorph.homeomorph_mk_coe

/-- Inverse of a homeomorphism. -/
protected def symm (h : Î± â‰ƒâ‚œ Î²) : Î² â‰ƒâ‚œ Î± where
  continuous_toFun := h.continuous_invFun
  continuous_invFun := h.continuous_toFun
  toEquiv := h.toEquiv.symm
#align homeomorph.symm Homeomorph.symm

@[simp] theorem symm_symm (h : Î± â‰ƒâ‚œ Î²) : h.symm.symm = h := rfl
#align homeomorph.symm_symm Homeomorph.symm_symm

/-- See Note [custom simps projection] -/
def Simps.symm_apply (h : Î± â‰ƒâ‚œ Î²) : Î² â†’ Î± :=
  h.symm
#align homeomorph.simps.symm_apply Homeomorph.Simps.symm_apply

initialize_simps_projections Homeomorph (toFun â†’ apply, invFun â†’ symm_apply)

@[simp]
theorem coe_toEquiv (h : Î± â‰ƒâ‚œ Î²) : â‡‘h.toEquiv = h :=
  rfl
#align homeomorph.coe_to_equiv Homeomorph.coe_toEquiv

@[simp]
theorem coe_symm_toEquiv (h : Î± â‰ƒâ‚œ Î²) : â‡‘h.toEquiv.symm = h.symm :=
  rfl
#align homeomorph.coe_symm_to_equiv Homeomorph.coe_symm_toEquiv

@[ext]
theorem ext {h h' : Î± â‰ƒâ‚œ Î²} (H : âˆ€ x, h x = h' x) : h = h' :=
  FunLike.ext _ _ H
#align homeomorph.ext Homeomorph.ext

/-- Identity map as a homeomorphism. -/
@[simps! (config := { fullyApplied := false }) apply]
protected def refl (Î± : Type*) [TopologicalSpace Î±] : Î± â‰ƒâ‚œ Î± where
  continuous_toFun := continuous_id
  continuous_invFun := continuous_id
  toEquiv := Equiv.refl Î±
#align homeomorph.refl Homeomorph.refl

/-- Composition of two homeomorphisms. -/
protected def trans (hâ‚ : Î± â‰ƒâ‚œ Î²) (hâ‚‚ : Î² â‰ƒâ‚œ Î³) : Î± â‰ƒâ‚œ Î³ where
  continuous_toFun := hâ‚‚.continuous_toFun.comp hâ‚.continuous_toFun
  continuous_invFun := hâ‚.continuous_invFun.comp hâ‚‚.continuous_invFun
  toEquiv := Equiv.trans hâ‚.toEquiv hâ‚‚.toEquiv
#align homeomorph.trans Homeomorph.trans

@[simp]
theorem trans_apply (hâ‚ : Î± â‰ƒâ‚œ Î²) (hâ‚‚ : Î² â‰ƒâ‚œ Î³) (a : Î±) : hâ‚.trans hâ‚‚ a = hâ‚‚ (hâ‚ a) :=
  rfl
#align homeomorph.trans_apply Homeomorph.trans_apply

@[simp] theorem symm_trans_apply (f : Î± â‰ƒâ‚œ Î²) (g : Î² â‰ƒâ‚œ Î³) (a : Î³) :
    (f.trans g).symm a = f.symm (g.symm a) := rfl

@[simp]
theorem homeomorph_mk_coe_symm (a : Equiv Î± Î²) (b c) :
    ((Homeomorph.mk a b c).symm : Î² â†’ Î±) = a.symm :=
  rfl
#align homeomorph.homeomorph_mk_coe_symm Homeomorph.homeomorph_mk_coe_symm

@[simp]
theorem refl_symm : (Homeomorph.refl Î±).symm = Homeomorph.refl Î± :=
  rfl
#align homeomorph.refl_symm Homeomorph.refl_symm

@[continuity]
protected theorem continuous (h : Î± â‰ƒâ‚œ Î²) : Continuous h :=
  h.continuous_toFun
#align homeomorph.continuous Homeomorph.continuous

-- otherwise `by continuity` can't prove continuity of `h.to_equiv.symm`
@[continuity]
protected theorem continuous_symm (h : Î± â‰ƒâ‚œ Î²) : Continuous h.symm :=
  h.continuous_invFun
#align homeomorph.continuous_symm Homeomorph.continuous_symm

@[simp]
theorem apply_symm_apply (h : Î± â‰ƒâ‚œ Î²) (x : Î²) : h (h.symm x) = x :=
  h.toEquiv.apply_symm_apply x
#align homeomorph.apply_symm_apply Homeomorph.apply_symm_apply

@[simp]
theorem symm_apply_apply (h : Î± â‰ƒâ‚œ Î²) (x : Î±) : h.symm (h x) = x :=
  h.toEquiv.symm_apply_apply x
#align homeomorph.symm_apply_apply Homeomorph.symm_apply_apply

@[simp]
theorem self_trans_symm (h : Î± â‰ƒâ‚œ Î²) : h.trans h.symm = Homeomorph.refl Î± := by
  ext
  apply symm_apply_apply
#align homeomorph.self_trans_symm Homeomorph.self_trans_symm

@[simp]
theorem symm_trans_self (h : Î± â‰ƒâ‚œ Î²) : h.symm.trans h = Homeomorph.refl Î² := by
  ext
  apply apply_symm_apply
#align homeomorph.symm_trans_self Homeomorph.symm_trans_self

protected theorem bijective (h : Î± â‰ƒâ‚œ Î²) : Function.Bijective h :=
  h.toEquiv.bijective
#align homeomorph.bijective Homeomorph.bijective

protected theorem injective (h : Î± â‰ƒâ‚œ Î²) : Function.Injective h :=
  h.toEquiv.injective
#align homeomorph.injective Homeomorph.injective

protected theorem surjective (h : Î± â‰ƒâ‚œ Î²) : Function.Surjective h :=
  h.toEquiv.surjective
#align homeomorph.surjective Homeomorph.surjective

/-- Change the homeomorphism `f` to make the inverse function definitionally equal to `g`. -/
def changeInv (f : Î± â‰ƒâ‚œ Î²) (g : Î² â†’ Î±) (hg : Function.RightInverse g f) : Î± â‰ƒâ‚œ Î² :=
  haveI : g = f.symm := (f.left_inv.eq_rightInverse hg).symm
  { toFun := f
    invFun := g
    left_inv := by convert f.left_inv
    right_inv := by convert f.right_inv using 1
    continuous_toFun := f.continuous
    continuous_invFun := by convert f.symm.continuous }
#align homeomorph.change_inv Homeomorph.changeInv

@[simp]
theorem symm_comp_self (h : Î± â‰ƒâ‚œ Î²) : h.symm âˆ˜ h = id :=
  funext h.symm_apply_apply
#align homeomorph.symm_comp_self Homeomorph.symm_comp_self

@[simp]
theorem self_comp_symm (h : Î± â‰ƒâ‚œ Î²) : h âˆ˜ h.symm = id :=
  funext h.apply_symm_apply
#align homeomorph.self_comp_symm Homeomorph.self_comp_symm

@[simp]
theorem range_coe (h : Î± â‰ƒâ‚œ Î²) : range h = univ :=
  h.surjective.range_eq
#align homeomorph.range_coe Homeomorph.range_coe

theorem image_symm (h : Î± â‰ƒâ‚œ Î²) : image h.symm = preimage h :=
  funext h.symm.toEquiv.image_eq_preimage
#align homeomorph.image_symm Homeomorph.image_symm

theorem preimage_symm (h : Î± â‰ƒâ‚œ Î²) : preimage h.symm = image h :=
  (funext h.toEquiv.image_eq_preimage).symm
#align homeomorph.preimage_symm Homeomorph.preimage_symm

@[simp]
theorem image_preimage (h : Î± â‰ƒâ‚œ Î²) (s : Set Î²) : h '' (h â»Â¹' s) = s :=
  h.toEquiv.image_preimage s
#align homeomorph.image_preimage Homeomorph.image_preimage

@[simp]
theorem preimage_image (h : Î± â‰ƒâ‚œ Î²) (s : Set Î±) : h â»Â¹' (h '' s) = s :=
  h.toEquiv.preimage_image s
#align homeomorph.preimage_image Homeomorph.preimage_image

protected theorem inducing (h : Î± â‰ƒâ‚œ Î²) : Inducing h :=
  inducing_of_inducing_compose h.continuous h.symm.continuous <| by
    simp only [symm_comp_self, inducing_id]
#align homeomorph.inducing Homeomorph.inducing

theorem induced_eq (h : Î± â‰ƒâ‚œ Î²) : TopologicalSpace.induced h â€¹_â€º = â€¹_â€º :=
  h.inducing.1.symm
#align homeomorph.induced_eq Homeomorph.induced_eq

protected theorem quotientMap (h : Î± â‰ƒâ‚œ Î²) : QuotientMap h :=
  QuotientMap.of_quotientMap_compose h.symm.continuous h.continuous <| by
    simp only [self_comp_symm, QuotientMap.id]
#align homeomorph.quotient_map Homeomorph.quotientMap

theorem coinduced_eq (h : Î± â‰ƒâ‚œ Î²) : TopologicalSpace.coinduced h â€¹_â€º = â€¹_â€º :=
  h.quotientMap.2.symm
#align homeomorph.coinduced_eq Homeomorph.coinduced_eq

protected theorem embedding (h : Î± â‰ƒâ‚œ Î²) : Embedding h :=
  âŸ¨h.inducing, h.injectiveâŸ©
#align homeomorph.embedding Homeomorph.embedding

/-- Homeomorphism given an embedding. -/
noncomputable def ofEmbedding (f : Î± â†’ Î²) (hf : Embedding f) : Î± â‰ƒâ‚œ Set.range f where
  continuous_toFun := hf.continuous.subtype_mk _
  continuous_invFun := hf.continuous_iff.2 <| by simp [continuous_subtype_val]
  toEquiv := Equiv.ofInjective f hf.inj
#align homeomorph.of_embedding Homeomorph.ofEmbedding

protected theorem secondCountableTopology [TopologicalSpace.SecondCountableTopology Î²]
    (h : Î± â‰ƒâ‚œ Î²) : TopologicalSpace.SecondCountableTopology Î± :=
  h.inducing.secondCountableTopology
#align homeomorph.second_countable_topology Homeomorph.secondCountableTopology

theorem isCompact_image {s : Set Î±} (h : Î± â‰ƒâ‚œ Î²) : IsCompact (h '' s) â†” IsCompact s :=
  h.embedding.isCompact_iff_isCompact_image.symm
#align homeomorph.is_compact_image Homeomorph.isCompact_image

theorem isCompact_preimage {s : Set Î²} (h : Î± â‰ƒâ‚œ Î²) : IsCompact (h â»Â¹' s) â†” IsCompact s := by
  rw [â† image_symm]; exact h.symm.isCompact_image
#align homeomorph.is_compact_preimage Homeomorph.isCompact_preimage

@[simp]
theorem isPreconnected_image {s : Set Î±} (h : Î± â‰ƒâ‚œ Î²) :
    IsPreconnected (h '' s) â†” IsPreconnected s :=
  âŸ¨fun hs â†¦ by simpa only [image_symm, preimage_image]
    using hs.image _ h.symm.continuous.continuousOn,
    fun hs â†¦ hs.image _ h.continuous.continuousOnâŸ©

@[simp]
theorem isPreconnected_preimage {s : Set Î²} (h : Î± â‰ƒâ‚œ Î²) :
    IsPreconnected (h â»Â¹' s) â†” IsPreconnected s := by
  rw [â† image_symm, isPreconnected_image]

@[simp]
theorem isConnected_image {s : Set Î±} (h : Î± â‰ƒâ‚œ Î²) :
    IsConnected (h '' s) â†” IsConnected s :=
  nonempty_image_iff.and h.isPreconnected_image

@[simp]
theorem isConnected_preimage {s : Set Î²} (h : Î± â‰ƒâ‚œ Î²) :
    IsConnected (h â»Â¹' s) â†” IsConnected s := by
  rw [â† image_symm, isConnected_image]

@[simp]
theorem comap_cocompact (h : Î± â‰ƒâ‚œ Î²) : comap h (cocompact Î²) = cocompact Î± :=
  (comap_cocompact_le h.continuous).antisymm <|
    (hasBasis_cocompact.le_basis_iff (hasBasis_cocompact.comap h)).2 fun K hK =>
      âŸ¨h â»Â¹' K, h.isCompact_preimage.2 hK, Subset.rflâŸ©
#align homeomorph.comap_cocompact Homeomorph.comap_cocompact

@[simp]
theorem map_cocompact (h : Î± â‰ƒâ‚œ Î²) : map h (cocompact Î±) = cocompact Î² := by
  rw [â† h.comap_cocompact, map_comap_of_surjective h.surjective]
#align homeomorph.map_cocompact Homeomorph.map_cocompact

protected theorem compactSpace [CompactSpace Î±] (h : Î± â‰ƒâ‚œ Î²) : CompactSpace Î² where
  isCompact_univ := h.symm.isCompact_preimage.2 isCompact_univ
#align homeomorph.compact_space Homeomorph.compactSpace

protected theorem t0Space [T0Space Î±] (h : Î± â‰ƒâ‚œ Î²) : T0Space Î² :=
  h.symm.embedding.t0Space
#align homeomorph.t0_space Homeomorph.t0Space

protected theorem t1Space [T1Space Î±] (h : Î± â‰ƒâ‚œ Î²) : T1Space Î² :=
  h.symm.embedding.t1Space
#align homeomorph.t1_space Homeomorph.t1Space

protected theorem t2Space [T2Space Î±] (h : Î± â‰ƒâ‚œ Î²) : T2Space Î² :=
  h.symm.embedding.t2Space
#align homeomorph.t2_space Homeomorph.t2Space

protected theorem t3Space [T3Space Î±] (h : Î± â‰ƒâ‚œ Î²) : T3Space Î² :=
  h.symm.embedding.t3Space
#align homeomorph.t3_space Homeomorph.t3Space

protected theorem denseEmbedding (h : Î± â‰ƒâ‚œ Î²) : DenseEmbedding h :=
  { h.embedding with dense := h.surjective.denseRange }
#align homeomorph.dense_embedding Homeomorph.denseEmbedding

@[simp]
theorem isOpen_preimage (h : Î± â‰ƒâ‚œ Î²) {s : Set Î²} : IsOpen (h â»Â¹' s) â†” IsOpen s :=
  h.quotientMap.isOpen_preimage
#align homeomorph.is_open_preimage Homeomorph.isOpen_preimage

@[simp]
theorem isOpen_image (h : Î± â‰ƒâ‚œ Î²) {s : Set Î±} : IsOpen (h '' s) â†” IsOpen s := by
  rw [â† preimage_symm, isOpen_preimage]
#align homeomorph.is_open_image Homeomorph.isOpen_image

protected theorem isOpenMap (h : Î± â‰ƒâ‚œ Î²) : IsOpenMap h := fun _ => h.isOpen_image.2
#align homeomorph.is_open_map Homeomorph.isOpenMap

@[simp]
theorem isClosed_preimage (h : Î± â‰ƒâ‚œ Î²) {s : Set Î²} : IsClosed (h â»Â¹' s) â†” IsClosed s := by
  simp only [â† isOpen_compl_iff, â† preimage_compl, isOpen_preimage]
#align homeomorph.is_closed_preimage Homeomorph.isClosed_preimage

@[simp]
theorem isClosed_image (h : Î± â‰ƒâ‚œ Î²) {s : Set Î±} : IsClosed (h '' s) â†” IsClosed s := by
  rw [â† preimage_symm, isClosed_preimage]
#align homeomorph.is_closed_image Homeomorph.isClosed_image

protected theorem isClosedMap (h : Î± â‰ƒâ‚œ Î²) : IsClosedMap h := fun _ => h.isClosed_image.2
#align homeomorph.is_closed_map Homeomorph.isClosedMap

protected theorem openEmbedding (h : Î± â‰ƒâ‚œ Î²) : OpenEmbedding h :=
  openEmbedding_of_embedding_open h.embedding h.isOpenMap
#align homeomorph.open_embedding Homeomorph.openEmbedding

protected theorem closedEmbedding (h : Î± â‰ƒâ‚œ Î²) : ClosedEmbedding h :=
  closedEmbedding_of_embedding_closed h.embedding h.isClosedMap
#align homeomorph.closed_embedding Homeomorph.closedEmbedding

protected theorem normalSpace [NormalSpace Î±] (h : Î± â‰ƒâ‚œ Î²) : NormalSpace Î² :=
  h.symm.closedEmbedding.normalSpace
#align homeomorph.normal_space Homeomorph.normalSpace

theorem preimage_closure (h : Î± â‰ƒâ‚œ Î²) (s : Set Î²) : h â»Â¹' closure s = closure (h â»Â¹' s) :=
  h.isOpenMap.preimage_closure_eq_closure_preimage h.continuous _
#align homeomorph.preimage_closure Homeomorph.preimage_closure

theorem image_closure (h : Î± â‰ƒâ‚œ Î²) (s : Set Î±) : h '' closure s = closure (h '' s) := by
  rw [â† preimage_symm, preimage_closure]
#align homeomorph.image_closure Homeomorph.image_closure

theorem preimage_interior (h : Î± â‰ƒâ‚œ Î²) (s : Set Î²) : h â»Â¹' interior s = interior (h â»Â¹' s) :=
  h.isOpenMap.preimage_interior_eq_interior_preimage h.continuous _
#align homeomorph.preimage_interior Homeomorph.preimage_interior

theorem image_interior (h : Î± â‰ƒâ‚œ Î²) (s : Set Î±) : h '' interior s = interior (h '' s) := by
  rw [â† preimage_symm, preimage_interior]
#align homeomorph.image_interior Homeomorph.image_interior

theorem preimage_frontier (h : Î± â‰ƒâ‚œ Î²) (s : Set Î²) : h â»Â¹' frontier s = frontier (h â»Â¹' s) :=
  h.isOpenMap.preimage_frontier_eq_frontier_preimage h.continuous _
#align homeomorph.preimage_frontier Homeomorph.preimage_frontier

theorem image_frontier (h : Î± â‰ƒâ‚œ Î²) (s : Set Î±) : h '' frontier s = frontier (h '' s) := by
  rw [â† preimage_symm, preimage_frontier]
#align homeomorph.image_frontier Homeomorph.image_frontier

@[to_additive]
theorem _root_.HasCompactMulSupport.comp_homeomorph {M} [One M] {f : Î² â†’ M}
    (hf : HasCompactMulSupport f) (Ï† : Î± â‰ƒâ‚œ Î²) : HasCompactMulSupport (f âˆ˜ Ï†) :=
  hf.comp_closedEmbedding Ï†.closedEmbedding
#align has_compact_mul_support.comp_homeomorph HasCompactMulSupport.comp_homeomorph
#align has_compact_support.comp_homeomorph HasCompactSupport.comp_homeomorph

@[simp]
theorem map_nhds_eq (h : Î± â‰ƒâ‚œ Î²) (x : Î±) : map h (ð“ x) = ð“ (h x) :=
  h.embedding.map_nhds_of_mem _ (by simp)
#align homeomorph.map_nhds_eq Homeomorph.map_nhds_eq

theorem symm_map_nhds_eq (h : Î± â‰ƒâ‚œ Î²) (x : Î±) : map h.symm (ð“ (h x)) = ð“ x := by
  rw [h.symm.map_nhds_eq, h.symm_apply_apply]
#align homeomorph.symm_map_nhds_eq Homeomorph.symm_map_nhds_eq

theorem nhds_eq_comap (h : Î± â‰ƒâ‚œ Î²) (x : Î±) : ð“ x = comap h (ð“ (h x)) :=
  h.inducing.nhds_eq_comap x
#align homeomorph.nhds_eq_comap Homeomorph.nhds_eq_comap

@[simp]
theorem comap_nhds_eq (h : Î± â‰ƒâ‚œ Î²) (y : Î²) : comap h (ð“ y) = ð“ (h.symm y) := by
  rw [h.nhds_eq_comap, h.apply_symm_apply]
#align homeomorph.comap_nhds_eq Homeomorph.comap_nhds_eq

/-- If the codomain of a homeomorphism is a locally connected space, then the domain is also
a locally connected space. -/
theorem locallyConnectedSpace [i : LocallyConnectedSpace Î²] (h : Î± â‰ƒâ‚œ Î²) :
    LocallyConnectedSpace Î± := by
  have : âˆ€ x, (ð“ x).HasBasis (fun s â†¦ IsOpen s âˆ§ h x âˆˆ s âˆ§ IsConnected s)
      (h.symm '' Â·) := fun x â†¦ by
    rw [â† h.symm_map_nhds_eq]
    exact (i.1 _).map _
  refine locallyConnectedSpace_of_connected_bases _ _ this fun _ _ hs â†¦ ?_
  exact hs.2.2.2.image _ h.symm.continuous.continuousOn

/-- If a bijective map `e : Î± â‰ƒ Î²` is continuous and open, then it is a homeomorphism. -/
def homeomorphOfContinuousOpen (e : Î± â‰ƒ Î²) (hâ‚ : Continuous e) (hâ‚‚ : IsOpenMap e) : Î± â‰ƒâ‚œ Î² where
  continuous_toFun := hâ‚
  continuous_invFun := by
    rw [continuous_def]
    intro s hs
    convert â† hâ‚‚ s hs using 1
    apply e.image_eq_preimage
  toEquiv := e
#align homeomorph.homeomorph_of_continuous_open Homeomorph.homeomorphOfContinuousOpen

@[simp]
theorem comp_continuousOn_iff (h : Î± â‰ƒâ‚œ Î²) (f : Î³ â†’ Î±) (s : Set Î³) :
    ContinuousOn (h âˆ˜ f) s â†” ContinuousOn f s :=
  h.inducing.continuousOn_iff.symm
#align homeomorph.comp_continuous_on_iff Homeomorph.comp_continuousOn_iff

@[simp]
theorem comp_continuous_iff (h : Î± â‰ƒâ‚œ Î²) {f : Î³ â†’ Î±} : Continuous (h âˆ˜ f) â†” Continuous f :=
  h.inducing.continuous_iff.symm
#align homeomorph.comp_continuous_iff Homeomorph.comp_continuous_iff

@[simp]
theorem comp_continuous_iff' (h : Î± â‰ƒâ‚œ Î²) {f : Î² â†’ Î³} : Continuous (f âˆ˜ h) â†” Continuous f :=
  h.quotientMap.continuous_iff.symm
#align homeomorph.comp_continuous_iff' Homeomorph.comp_continuous_iff'

theorem comp_continuousAt_iff (h : Î± â‰ƒâ‚œ Î²) (f : Î³ â†’ Î±) (x : Î³) :
    ContinuousAt (h âˆ˜ f) x â†” ContinuousAt f x :=
  h.inducing.continuousAt_iff.symm
#align homeomorph.comp_continuous_at_iff Homeomorph.comp_continuousAt_iff

theorem comp_continuousAt_iff' (h : Î± â‰ƒâ‚œ Î²) (f : Î² â†’ Î³) (x : Î±) :
    ContinuousAt (f âˆ˜ h) x â†” ContinuousAt f (h x) :=
  h.inducing.continuousAt_iff' (by simp)
#align homeomorph.comp_continuous_at_iff' Homeomorph.comp_continuousAt_iff'

theorem comp_continuousWithinAt_iff (h : Î± â‰ƒâ‚œ Î²) (f : Î³ â†’ Î±) (s : Set Î³) (x : Î³) :
    ContinuousWithinAt f s x â†” ContinuousWithinAt (h âˆ˜ f) s x :=
  h.inducing.continuousWithinAt_iff
#align homeomorph.comp_continuous_within_at_iff Homeomorph.comp_continuousWithinAt_iff

@[simp]
theorem comp_isOpenMap_iff (h : Î± â‰ƒâ‚œ Î²) {f : Î³ â†’ Î±} : IsOpenMap (h âˆ˜ f) â†” IsOpenMap f := by
  refine' âŸ¨_, fun hf => h.isOpenMap.comp hfâŸ©
  intro hf
  rw [â† Function.comp.left_id f, â† h.symm_comp_self, Function.comp.assoc]
  exact h.symm.isOpenMap.comp hf
#align homeomorph.comp_is_open_map_iff Homeomorph.comp_isOpenMap_iff

@[simp]
theorem comp_isOpenMap_iff' (h : Î± â‰ƒâ‚œ Î²) {f : Î² â†’ Î³} : IsOpenMap (f âˆ˜ h) â†” IsOpenMap f := by
  refine' âŸ¨_, fun hf => hf.comp h.isOpenMapâŸ©
  intro hf
  rw [â† Function.comp.right_id f, â† h.self_comp_symm, â† Function.comp.assoc]
  exact hf.comp h.symm.isOpenMap
#align homeomorph.comp_is_open_map_iff' Homeomorph.comp_isOpenMap_iff'

/-- If two sets are equal, then they are homeomorphic. -/
def setCongr {s t : Set Î±} (h : s = t) : s â‰ƒâ‚œ t where
  continuous_toFun := continuous_inclusion h.subset
  continuous_invFun := continuous_inclusion h.symm.subset
  toEquiv := Equiv.setCongr h
#align homeomorph.set_congr Homeomorph.setCongr

/-- Sum of two homeomorphisms. -/
def sumCongr (hâ‚ : Î± â‰ƒâ‚œ Î²) (hâ‚‚ : Î³ â‰ƒâ‚œ Î´) : Sum Î± Î³ â‰ƒâ‚œ Sum Î² Î´ where
  continuous_toFun := hâ‚.continuous.sum_map hâ‚‚.continuous
  continuous_invFun := hâ‚.symm.continuous.sum_map hâ‚‚.symm.continuous
  toEquiv := hâ‚.toEquiv.sumCongr hâ‚‚.toEquiv
#align homeomorph.sum_congr Homeomorph.sumCongr

/-- Product of two homeomorphisms. -/
def prodCongr (hâ‚ : Î± â‰ƒâ‚œ Î²) (hâ‚‚ : Î³ â‰ƒâ‚œ Î´) : Î± Ã— Î³ â‰ƒâ‚œ Î² Ã— Î´ where
  continuous_toFun := hâ‚.continuous.prod_map hâ‚‚.continuous
  continuous_invFun := hâ‚.symm.continuous.prod_map hâ‚‚.symm.continuous
  toEquiv := hâ‚.toEquiv.prodCongr hâ‚‚.toEquiv
#align homeomorph.prod_congr Homeomorph.prodCongr

@[simp]
theorem prodCongr_symm (hâ‚ : Î± â‰ƒâ‚œ Î²) (hâ‚‚ : Î³ â‰ƒâ‚œ Î´) :
    (hâ‚.prodCongr hâ‚‚).symm = hâ‚.symm.prodCongr hâ‚‚.symm :=
  rfl
#align homeomorph.prod_congr_symm Homeomorph.prodCongr_symm

@[simp]
theorem coe_prodCongr (hâ‚ : Î± â‰ƒâ‚œ Î²) (hâ‚‚ : Î³ â‰ƒâ‚œ Î´) : â‡‘(hâ‚.prodCongr hâ‚‚) = Prod.map hâ‚ hâ‚‚ :=
  rfl
#align homeomorph.coe_prod_congr Homeomorph.coe_prodCongr

section

variable (Î± Î² Î³)

/-- `Î± Ã— Î²` is homeomorphic to `Î² Ã— Î±`. -/
def prodComm : Î± Ã— Î² â‰ƒâ‚œ Î² Ã— Î± where
  continuous_toFun := continuous_snd.prod_mk continuous_fst
  continuous_invFun := continuous_snd.prod_mk continuous_fst
  toEquiv := Equiv.prodComm Î± Î²
#align homeomorph.prod_comm Homeomorph.prodComm

@[simp]
theorem prodComm_symm : (prodComm Î± Î²).symm = prodComm Î² Î± :=
  rfl
#align homeomorph.prod_comm_symm Homeomorph.prodComm_symm

@[simp]
theorem coe_prodComm : â‡‘(prodComm Î± Î²) = Prod.swap :=
  rfl
#align homeomorph.coe_prod_comm Homeomorph.coe_prodComm

/-- `(Î± Ã— Î²) Ã— Î³` is homeomorphic to `Î± Ã— (Î² Ã— Î³)`. -/
def prodAssoc : (Î± Ã— Î²) Ã— Î³ â‰ƒâ‚œ Î± Ã— Î² Ã— Î³ where
  continuous_toFun := continuous_fst.fst.prod_mk (continuous_fst.snd.prod_mk continuous_snd)
  continuous_invFun := (continuous_fst.prod_mk continuous_snd.fst).prod_mk continuous_snd.snd
  toEquiv := Equiv.prodAssoc Î± Î² Î³
#align homeomorph.prod_assoc Homeomorph.prodAssoc

/-- `Î± Ã— {*}` is homeomorphic to `Î±`. -/
@[simps! (config := { fullyApplied := false }) apply]
def prodPUnit : Î± Ã— PUnit â‰ƒâ‚œ Î± where
  toEquiv := Equiv.prodPUnit Î±
  continuous_toFun := continuous_fst
  continuous_invFun := continuous_id.prod_mk continuous_const
#align homeomorph.prod_punit Homeomorph.prodPUnit

/-- `{*} Ã— Î±` is homeomorphic to `Î±`. -/
def punitProd : PUnit Ã— Î± â‰ƒâ‚œ Î± :=
  (prodComm _ _).trans (prodPUnit _)
#align homeomorph.punit_prod Homeomorph.punitProd

@[simp] theorem coe_punitProd : â‡‘(punitProd Î±) = Prod.snd := rfl
#align homeomorph.coe_punit_prod Homeomorph.coe_punitProd

/-- If both `Î±` and `Î²` have a unique element, then `Î± â‰ƒâ‚œ Î²`. -/
@[simps!]
def homeomorphOfUnique [Unique Î±] [Unique Î²] : Î± â‰ƒâ‚œ Î² :=
  { Equiv.equivOfUnique Î± Î² with
    continuous_toFun := continuous_const
    continuous_invFun := continuous_const }
#align homeomorph.homeomorph_of_unique Homeomorph.homeomorphOfUnique

end

/-- If each `Î²â‚ i` is homeomorphic to `Î²â‚‚ i`, then `Î  i, Î²â‚ i` is homeomorphic to `Î  i, Î²â‚‚ i`. -/
@[simps! apply toEquiv]
def piCongrRight {Î¹ : Type*} {Î²â‚ Î²â‚‚ : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (Î²â‚ i)]
    [âˆ€ i, TopologicalSpace (Î²â‚‚ i)] (F : âˆ€ i, Î²â‚ i â‰ƒâ‚œ Î²â‚‚ i) : (âˆ€ i, Î²â‚ i) â‰ƒâ‚œ âˆ€ i, Î²â‚‚ i where
  continuous_toFun := continuous_pi fun i => (F i).continuous.comp <| continuous_apply i
  continuous_invFun := continuous_pi fun i => (F i).symm.continuous.comp <| continuous_apply i
  toEquiv := Equiv.piCongrRight fun i => (F i).toEquiv
#align homeomorph.Pi_congr_right Homeomorph.piCongrRight

@[simp]
theorem piCongrRight_symm {Î¹ : Type*} {Î²â‚ Î²â‚‚ : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (Î²â‚ i)]
    [âˆ€ i, TopologicalSpace (Î²â‚‚ i)] (F : âˆ€ i, Î²â‚ i â‰ƒâ‚œ Î²â‚‚ i) :
    (piCongrRight F).symm = piCongrRight fun i => (F i).symm :=
  rfl
#align homeomorph.Pi_congr_right_symm Homeomorph.piCongrRight_symm

-- porting note: TODO: align the order of universes with `Equiv.ulift`
/-- `ULift Î±` is homeomorphic to `Î±`. -/
def ulift.{u, v} {Î± : Type u} [TopologicalSpace Î±] : ULift.{v, u} Î± â‰ƒâ‚œ Î± where
  continuous_toFun := continuous_uLift_down
  continuous_invFun := continuous_uLift_up
  toEquiv := Equiv.ulift
#align homeomorph.ulift Homeomorph.ulift

section Distrib

/-- `(Î± âŠ• Î²) Ã— Î³` is homeomorphic to `Î± Ã— Î³ âŠ• Î² Ã— Î³`. -/
def sumProdDistrib : Sum Î± Î² Ã— Î³ â‰ƒâ‚œ Sum (Î± Ã— Î³) (Î² Ã— Î³) :=
  Homeomorph.symm <|
    homeomorphOfContinuousOpen (Equiv.sumProdDistrib Î± Î² Î³).symm
        ((continuous_inl.prod_map continuous_id).sum_elim
          (continuous_inr.prod_map continuous_id)) <|
      (isOpenMap_inl.prod IsOpenMap.id).sum_elim (isOpenMap_inr.prod IsOpenMap.id)
#align homeomorph.sum_prod_distrib Homeomorph.sumProdDistrib

/-- `Î± Ã— (Î² âŠ• Î³)` is homeomorphic to `Î± Ã— Î² âŠ• Î± Ã— Î³`. -/
def prodSumDistrib : Î± Ã— Sum Î² Î³ â‰ƒâ‚œ Sum (Î± Ã— Î²) (Î± Ã— Î³) :=
  (prodComm _ _).trans <| sumProdDistrib.trans <| sumCongr (prodComm _ _) (prodComm _ _)
#align homeomorph.prod_sum_distrib Homeomorph.prodSumDistrib

variable {Î¹ : Type*} {Ïƒ : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (Ïƒ i)]

/-- `(Î£ i, Ïƒ i) Ã— Î²` is homeomorphic to `Î£ i, (Ïƒ i Ã— Î²)`. -/
def sigmaProdDistrib : (Î£i, Ïƒ i) Ã— Î² â‰ƒâ‚œ Î£i, Ïƒ i Ã— Î² :=
  Homeomorph.symm <|
    homeomorphOfContinuousOpen (Equiv.sigmaProdDistrib Ïƒ Î²).symm
      (continuous_sigma fun _ => continuous_sigmaMk.fst'.prod_mk continuous_snd)
      (isOpenMap_sigma.2 fun _ => isOpenMap_sigmaMk.prod IsOpenMap.id)
#align homeomorph.sigma_prod_distrib Homeomorph.sigmaProdDistrib

end Distrib

/-- If `Î¹` has a unique element, then `Î¹ â†’ Î±` is homeomorphic to `Î±`. -/
@[simps! (config := { fullyApplied := false })]
def funUnique (Î¹ Î± : Type*) [Unique Î¹] [TopologicalSpace Î±] : (Î¹ â†’ Î±) â‰ƒâ‚œ Î± where
  toEquiv := Equiv.funUnique Î¹ Î±
  continuous_toFun := continuous_apply _
  continuous_invFun := continuous_pi fun _ => continuous_id
#align homeomorph.fun_unique Homeomorph.funUnique

/-- Homeomorphism between dependent functions `Î  i : Fin 2, Î± i` and `Î± 0 Ã— Î± 1`. -/
@[simps! (config := { fullyApplied := false })]
def piFinTwo.{u} (Î± : Fin 2 â†’ Type u) [âˆ€ i, TopologicalSpace (Î± i)] : (âˆ€ i, Î± i) â‰ƒâ‚œ Î± 0 Ã— Î± 1 where
  toEquiv := piFinTwoEquiv Î±
  continuous_toFun := (continuous_apply 0).prod_mk (continuous_apply 1)
  continuous_invFun := continuous_pi <| Fin.forall_fin_two.2 âŸ¨continuous_fst, continuous_sndâŸ©
#align homeomorph.pi_fin_two Homeomorph.piFinTwo

/-- Homeomorphism between `Î±Â² = Fin 2 â†’ Î±` and `Î± Ã— Î±`. -/
@[simps! (config := { fullyApplied := false })]
def finTwoArrow : (Fin 2 â†’ Î±) â‰ƒâ‚œ Î± Ã— Î± :=
  { piFinTwo fun _ => Î± with toEquiv := finTwoArrowEquiv Î± }
#align homeomorph.fin_two_arrow Homeomorph.finTwoArrow

/-- A subset of a topological space is homeomorphic to its image under a homeomorphism.
-/
@[simps!]
def image (e : Î± â‰ƒâ‚œ Î²) (s : Set Î±) : s â‰ƒâ‚œ e '' s where
  -- porting note: todo: by continuity!
  continuous_toFun := e.continuous.continuousOn.restrict_mapsTo (mapsTo_image _ _)
  continuous_invFun := (e.symm.continuous.comp continuous_subtype_val).codRestrict _
  toEquiv := e.toEquiv.image s
#align homeomorph.image Homeomorph.image

/-- `Set.univ Î±` is homeomorphic to `Î±`. -/
@[simps! (config := { fullyApplied := false })]
def Set.univ (Î± : Type*) [TopologicalSpace Î±] : (univ : Set Î±) â‰ƒâ‚œ Î± where
  toEquiv := Equiv.Set.univ Î±
  continuous_toFun := continuous_subtype_val
  continuous_invFun := continuous_id.subtype_mk _
#align homeomorph.set.univ Homeomorph.Set.univ

/-- `s Ã—Ë¢ t` is homeomorphic to `s Ã— t`. -/
@[simps!]
def Set.prod (s : Set Î±) (t : Set Î²) : â†¥(s Ã—Ë¢ t) â‰ƒâ‚œ s Ã— t where
  toEquiv := Equiv.Set.prod s t
  continuous_toFun :=
    (continuous_subtype_val.fst.subtype_mk _).prod_mk (continuous_subtype_val.snd.subtype_mk _)
  continuous_invFun :=
    (continuous_subtype_val.fst'.prod_mk continuous_subtype_val.snd').subtype_mk _
#align homeomorph.set.prod Homeomorph.Set.prod

section

variable {Î¹ : Type*}

/-- The topological space `Î  i, Î² i` can be split as a product by separating the indices in Î¹
  depending on whether they satisfy a predicate p or not.-/
@[simps!]
def piEquivPiSubtypeProd (p : Î¹ â†’ Prop) (Î² : Î¹ â†’ Type*) [âˆ€ i, TopologicalSpace (Î² i)]
    [DecidablePred p] : (âˆ€ i, Î² i) â‰ƒâ‚œ (âˆ€ i : { x // p x }, Î² i) Ã— âˆ€ i : { x // Â¬p x }, Î² i
    where
  toEquiv := Equiv.piEquivPiSubtypeProd p Î²
  continuous_toFun := by
    apply Continuous.prod_mk <;> exact continuous_pi fun j => continuous_apply j.1
  continuous_invFun :=
    continuous_pi fun j => by
      dsimp only [Equiv.piEquivPiSubtypeProd]; split_ifs
      exacts [(continuous_apply _).comp continuous_fst, (continuous_apply _).comp continuous_snd]
#align homeomorph.pi_equiv_pi_subtype_prod Homeomorph.piEquivPiSubtypeProd

variable [DecidableEq Î¹] (i : Î¹)

/-- A product of topological spaces can be split as the binary product of one of the spaces and
  the product of all the remaining spaces. -/
@[simps!]
def piSplitAt (Î² : Î¹ â†’ Type*) [âˆ€ j, TopologicalSpace (Î² j)] :
    (âˆ€ j, Î² j) â‰ƒâ‚œ Î² i Ã— âˆ€ j : { j // j â‰  i }, Î² j
    where
  toEquiv := Equiv.piSplitAt i Î²
  continuous_toFun := (continuous_apply i).prod_mk (continuous_pi fun j => continuous_apply j.1)
  continuous_invFun :=
    continuous_pi fun j => by
      dsimp only [Equiv.piSplitAt]
      split_ifs with h
      subst h
      exacts [continuous_fst, (continuous_apply _).comp continuous_snd]
#align homeomorph.pi_split_at Homeomorph.piSplitAt

variable (Î²)

/-- A product of copies of a topological space can be split as the binary product of one copy and
  the product of all the remaining copies. -/
@[simps!]
def funSplitAt : (Î¹ â†’ Î²) â‰ƒâ‚œ Î² Ã— ({ j // j â‰  i } â†’ Î²) :=
  piSplitAt i _
#align homeomorph.fun_split_at Homeomorph.funSplitAt

end

end Homeomorph

/-- An inducing equiv between topological spaces is a homeomorphism. -/
@[simps toEquiv] -- porting note: TODO: was `@[simps]`
def Equiv.toHomeomorphOfInducing [TopologicalSpace Î±] [TopologicalSpace Î²] (f : Î± â‰ƒ Î²)
    (hf : Inducing f) : Î± â‰ƒâ‚œ Î² :=
  { f with
    continuous_toFun := hf.continuous
    continuous_invFun := hf.continuous_iff.2 <| by simpa using continuous_id }
#align equiv.to_homeomorph_of_inducing Equiv.toHomeomorphOfInducing

namespace Continuous

variable [TopologicalSpace Î±] [TopologicalSpace Î²]

theorem continuous_symm_of_equiv_compact_to_t2 [CompactSpace Î±] [T2Space Î²] {f : Î± â‰ƒ Î²}
    (hf : Continuous f) : Continuous f.symm := by
  rw [continuous_iff_isClosed]
  intro C hC
  have hC' : IsClosed (f '' C) := (hC.isCompact.image hf).isClosed
  rwa [Equiv.image_eq_preimage] at hC'
#align continuous.continuous_symm_of_equiv_compact_to_t2 Continuous.continuous_symm_of_equiv_compact_to_t2

/-- Continuous equivalences from a compact space to a T2 space are homeomorphisms.

This is not true when T2 is weakened to T1
(see `Continuous.homeoOfEquivCompactToT2.t1_counterexample`). -/
@[simps toEquiv] -- porting note: was `@[simps]`
def homeoOfEquivCompactToT2 [CompactSpace Î±] [T2Space Î²] {f : Î± â‰ƒ Î²} (hf : Continuous f) : Î± â‰ƒâ‚œ Î² :=
  { f with
    continuous_toFun := hf
    continuous_invFun := hf.continuous_symm_of_equiv_compact_to_t2 }
#align continuous.homeo_of_equiv_compact_to_t2 Continuous.homeoOfEquivCompactToT2

end Continuous
