/-
Copyright (c) 2024 Ben Eltschig. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ben Eltschig
-/
import Mathlib.Data.Nat.Log
import Mathlib.Topology.Path

/-!
# Countable concatenations of paths

In this file we provide a way to concatenate countably many paths leading up to a point into a
single path whenever that is possible. This is occasionally useful to construct a path passing
through some convergent sequence of points.

## Main definitions

* `Path.countableConcat Î³ x hÎ³x`: the concatenation of countably many paths `Î³ n` leading up to
  some point `x`, given that these paths converge in some precise sense (`hÎ³x`) to `x`.
* `Path.countableConcat_eq_trans`: the recurrence relation fulfilled by `Path.countableConcat`,
  showing that `Path.countableConcat Î³ x hÎ³x` is the concatenation of `Î³ 0` with the countable
  concatenation with the remaining paths.
* `Path.map_countableConcat`: countable concatenation commutes with `Path.map`, i.e. the image of a
  countable concatenation of paths is the concatenation of the images.
-/

noncomputable section

open Topology unitInterval Set Filter

namespace Path

variable {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] {s : â„• â†’ X}

/-- The concatenation of countably many paths leading up to some point `x` as a function. The
corresponding path is defined separately because continuity takes some effort to prove. -/
def countableConcatFun (Î³ : (n : â„•) â†’ Path (s n) (s (n + 1))) (x : X) : I â†’ X := fun t â†¦ by
  let n := Nat.log 2 âŒŠ(Ïƒ t).1â»Â¹âŒ‹â‚Š
  refine if ht : t < 1 then Î³ n âŸ¨2 * (1 - Ïƒ t * (2 ^ n : â„•)), ?_, ?_âŸ© else x
  <;> have ht' := symm_one â–¸ symm_lt_symm.2 ht <;> have ht'' := coe_pos.2 ht'
  Â· suffices (Ïƒ t : â„) * (2 ^ n : â„•) â‰¤ 1 by linarith
    calc
      _ â‰¤ (Ïƒ t).1 * âŒŠ(Ïƒ t).1â»Â¹âŒ‹â‚Š := ?_
      _ â‰¤ (Ïƒ t).1 * (Ïƒ t).1â»Â¹    := by gcongr; exact Nat.floor_le <| by simp [t.2.2]
      _ = 1                      := mul_inv_cancelâ‚€ ht''.ne'
    gcongr
    exact Nat.pow_log_le_self _ (Nat.floor_pos.2 <| (one_le_invâ‚€ ht'').2 (Ïƒ t).2.2).ne'
  Â· suffices h : 1 â‰¤ (Ïƒ t : â„) * (2 * (2 ^ n : â„•)) by rw [mul_left_comm] at h; linarith
    refine (mul_inv_cancelâ‚€ ht''.ne').symm.le.trans <|
      mul_le_mul_of_nonneg_left ?_ (Ïƒ t).2.1
    rw [â† Nat.cast_ofNat, â† Nat.cast_mul, â† Nat.pow_succ']
    exact (Nat.lt_succ_floor _).le.trans <| Nat.cast_le.2 <| Nat.succ_le_of_lt <|
      Nat.lt_pow_succ_log_self one_lt_two _

/-- On closed intervals [1 - 2 ^ n, 1 - 2 ^ (n + 1)], `countableConcatFun Î³ x` agrees with a
reparametrisation of `Î³ n`. -/
lemma countableConcatFun_eqOn (Î³ : (n : â„•) â†’ Path (s n) (s (n + 1))) {x : X} (n : â„•) :
    Set.EqOn (countableConcatFun Î³ x) (fun t â†¦ (Î³ n).extend (2 * (1 - (1 - t) * (2 ^ n))))
    (Set.Icc (Ïƒ âŸ¨(2 ^ n)â»Â¹, by simp [inv_le_oneâ‚€, one_le_powâ‚€]âŸ©)
      (Ïƒ âŸ¨(2 ^ (n+1))â»Â¹, by simp [inv_le_oneâ‚€, one_le_powâ‚€]âŸ©)) := fun t ht â†¦ by
  simp only [Set.mem_Icc, â† Subtype.coe_le_coe, coe_symm_eq] at ht
  have ht' : t < 1 := coe_lt_one.1 <| ht.2.trans_lt <| by simp
  have ht'' : 0 < 1 - t.1 := by linarith [coe_lt_one.2 ht']
  simp only [countableConcatFun, ht', â†“reduceDIte, coe_symm_eq]
  by_cases hn : Nat.log 2 âŒŠ(1 - t : â„)â»Â¹âŒ‹â‚Š = n
  Â· refine congr (by rw [hn]) ?_
    rw [Set.projIcc_of_mem _ <| Set.mem_Icc.1 âŸ¨?_, ?_âŸ©]
    Â· simp [hn]
    Â· have h := mul_le_mul_of_nonneg_right ht.1 (a := 2 ^ n) (by simp)
      rw [sub_mul, inv_mul_cancelâ‚€ (by simp)] at h
      linarith
    Â· have h := mul_le_mul_of_nonneg_right ht.2 (a := 2 ^ (n+1)) (by simp)
      rw [sub_mul, inv_mul_cancelâ‚€ (by simp), pow_succ] at h
      linarith
  Â· replace hn : Nat.log 2 âŒŠ(1 - t : â„)â»Â¹âŒ‹â‚Š = n + 1 := by
      refine le_antisymm ?_ <| n.succ_le_of_lt <| (Ne.symm hn).lt_of_le ?_
      Â· refine (Nat.log_mono_right <| Nat.floor_le_floor <| inv_antiâ‚€ (by simp) <|
          le_sub_comm.1 ht.2).trans ?_
        rw [â† Nat.cast_ofNat (R := â„), â† Nat.cast_pow, inv_inv, Nat.floor_natCast,
          Nat.log_pow one_lt_two _]
      Â· refine le_trans ?_ <| Nat.log_mono_right <| Nat.floor_le_floor <| inv_antiâ‚€ ht'' <|
          sub_le_comm.1 ht.1
        rw [â† Nat.cast_ofNat (R := â„), â† Nat.cast_pow, inv_inv, Nat.floor_natCast,
          Nat.log_pow one_lt_two _]
    have ht'' : 2 * (1 - (1 - t.1) * 2 ^ n) = 1 := by
      suffices h : t.1 = 1 - (2 ^ (n + 1))â»Â¹ by
        rw [h, pow_succ]; simp [mul_sub, show (2 : â„) - 1 = 1 by ring]
      refine le_antisymm ht.2 ?_
      rw [sub_le_comm, â† hn, â† Nat.cast_ofNat (R := â„), â† Nat.cast_pow]
      refine le_trans (by rw [inv_inv]) <| inv_antiâ‚€ (by simp) <| (Nat.cast_le.2 <|
        Nat.pow_log_le_self 2 ?_).trans <| Nat.floor_le (inv_pos.2 ht'').le
      exact (Nat.floor_pos.2 <| (one_le_invâ‚€ ht'').2 (Ïƒ t).2.2).ne'
    rw [ht'', extend_one]; convert (Î³ (n + 1)).source
    simp [hn, pow_succ]
    linarith

lemma countableConcatFun_one {Î³ : (n : â„•) â†’ Path (s n) (s (n + 1))} {x : X} :
    countableConcatFun Î³ x 1 = x := by
  simp [countableConcatFun]

/-- The concatenation of countably many paths `Î³ n` leading up to some point `x`. The condition
`hÎ³x` is the precise condition needed in order for the concatenation to be continuous at `1`. -/
def countableConcat (Î³ : (n : â„•) â†’ Path (s n) (s (n + 1))) (x : X)
    (hÎ³x : Tendsto (fun x : â„• Ã— I â†¦ Î³ x.1 x.2) (atTop Ã—Ë¢ âŠ¤) (ğ“ x)) : Path (s 0) x where
  toFun := countableConcatFun Î³ x
  continuous_toFun := by
    refine continuous_iff_continuousAt.2 fun t â†¦ ?_
    by_cases ht : t < 1
    Â· have ht' := symm_one â–¸ symm_lt_symm.2 ht; have ht'' := coe_pos.2 ht'
      have hÎ³' : âˆ€ n, ContinuousOn (countableConcatFun Î³ x) _ :=
        fun n â†¦ (Continuous.continuousOn (by continuity)).congr <| countableConcatFun_eqOn Î³ n
      cases h : Nat.log 2 âŒŠ(Ïƒ t : â„)â»Â¹âŒ‹â‚Š with
      | zero =>
        refine ContinuousOn.continuousAt (s := Set.Iic âŸ¨2â»Â¹, by simp, ?_âŸ©) ?_ ?_
        Â· convert hÎ³' 0 using 1
          rw [â† Set.Icc_bot, show (âŠ¥ : I) = 0 by rfl]; convert rfl using 2 <;> ext
          all_goals simp [show (1 : â„) - 2â»Â¹ = 2â»Â¹ by ring, (one_div (2 : â„)) â–¸ one_half_lt_one.le]
        Â· refine Iic_mem_nhds <| Subtype.coe_lt_coe.1 (?_ : t.1 < 2â»Â¹)
          rw [Nat.log_eq_zero_iff, or_iff_left one_lt_two.not_le, Nat.floor_lt (inv_pos.2 ht'').le,
            inv_lt_commâ‚€ (by exact ht'') two_pos, coe_symm_eq, lt_sub_comm] at h
          exact h.trans_eq (by ring)
      | succ n =>
        refine ContinuousOn.continuousAt (s := Set.Icc
          âŸ¨1 - (2 ^ n)â»Â¹, by simp [inv_le_one_of_one_leâ‚€ <| one_le_powâ‚€ one_le_two (Mâ‚€ := â„)]âŸ©
          âŸ¨1 - (2 ^ (n + 2))â»Â¹, by
            simp [inv_le_one_of_one_leâ‚€ <| one_le_powâ‚€ one_le_two (Mâ‚€ := â„)]âŸ©) ?_ ?_
        Â· convert (hÎ³' n).union_isClosed isClosed_Icc isClosed_Icc <| hÎ³' (n + 1) using 1
          rw [add_assoc, one_add_one_eq_two, Set.Icc_union_Icc_eq_Icc]
          Â· rfl
          Â· simp only [symm_le_symm, Subtype.mk_le_mk]
            exact inv_antiâ‚€ (by simp) <| pow_le_pow_rightâ‚€ one_le_two (by simp)
          Â· simp only [symm_le_symm, Subtype.mk_le_mk]
            exact inv_antiâ‚€ (by simp) <| pow_le_pow_rightâ‚€ one_le_two (by simp)
        Â· refine Icc_mem_nhds ?_ ?_ <;> rw [â† Subtype.coe_lt_coe, Subtype.coe_mk]
          Â· replace h := h.symm.le; rw [â† Nat.pow_le_iff_le_log one_lt_two (Nat.floor_pos.2 <|
              (one_le_invâ‚€ ht'').2 (Ïƒ t).2.2).ne', Nat.le_floor_iff (inv_pos.2 ht'').le,
              le_inv_commâ‚€ (by simp) ht'', coe_symm_eq, sub_le_comm] at h
            refine (sub_lt_sub_left (inv_strictAntiâ‚€ (by simp) ?_) 1).trans_le h
            rw [Nat.cast_pow, Nat.cast_ofNat]
            exact pow_lt_pow_rightâ‚€ one_lt_two n.lt_succ_self
          Â· replace h := h.trans_lt (Nat.lt_succ_self _); rw [â† Nat.lt_pow_iff_log_lt one_lt_two
              (Nat.floor_pos.2 <| (one_le_invâ‚€ ht'').2 (Ïƒ t).2.2).ne', Nat.floor_lt
              (inv_pos.2 ht'').le, inv_lt_commâ‚€ ht'' (by simp), coe_symm_eq, lt_sub_comm] at h
            exact h.trans_eq <| by simp
    Â· rw [unitInterval.lt_one_iff_ne_one, not_ne_iff] at ht; rw [ht]
      unfold ContinuousAt
      intro u hu
      rw [countableConcatFun_one] at hu
      let âŸ¨n, hnâŸ© : âˆƒ n : â„•, âˆ€ m, n â‰¤ m â†’ âˆ€ t, Î³ m t âˆˆ u := by
        simpa [tendsto_def, mem_prod_top] using hÎ³x hu
      rw [mem_map, mem_nhds_iff]
      use Set.Ioi âŸ¨1 - (2 ^ n)â»Â¹, by rw [sub_nonneg, inv_le_oneâ‚€] <;> simp [one_le_powâ‚€], by simpâŸ©
      refine âŸ¨fun t ht â†¦ ?_, isOpen_Ioi, by simp [â† coe_lt_one]âŸ©
      by_cases ht' : t < 1
      Â· have ht'' := symm_one â–¸ symm_lt_symm.2 ht'; have ht''' := coe_pos.2 ht''
        simp only [mem_preimage, countableConcatFun, ht', reduceDIte]
        refine hn _ ?_ _
        rw [â† Nat.pow_le_iff_le_log one_lt_two (Nat.floor_pos.2 <| (one_le_invâ‚€ ht''').2
          (Ïƒ t).2.2).ne', Nat.le_floor_iff (inv_pos.2 ht''').le, le_inv_commâ‚€ (by simp) ht''',
          coe_symm_eq, sub_le_comm]
        apply le_of_lt; simpa using ht
      Â· rw [unitInterval.lt_one_iff_ne_one, not_ne_iff] at ht'; rw [ht']
        rw [mem_preimage, countableConcatFun_one]; exact mem_of_mem_nhds hu
  source' := by simp [countableConcatFun]
  target' := by simp [countableConcatFun]

private lemma one_sub_half_div_two_pow_mem_unitInterval {t : I} {n : â„•} :
    (1 - (t : â„) / 2) / 2 ^ n âˆˆ I :=
  âŸ¨div_nonneg (by linarith [t.2.2]) (by simp), (div_le_oneâ‚€ (by simp)).2 <| by
    linarith [one_le_powâ‚€ (Mâ‚€ := â„) one_le_two (n := n), t.2.1]âŸ©

/-- Evaluating `Path.countableConcat` at 1-(1-t/2)/2^n yields `Î³ n t`. -/
lemma countableConcat_applyAt {Î³ : (n : â„•) â†’ Path (s n) (s (n + 1))} {x : X}
    (hÎ³x : Tendsto (fun x : â„• Ã— I â†¦ Î³ x.1 x.2) (atTop Ã—Ë¢ âŠ¤) (ğ“ x)) (n : â„•) (t : I) :
    countableConcat Î³ x hÎ³x (Ïƒ âŸ¨(1 - t / 2) / 2 ^ n, one_sub_half_div_two_pow_mem_unitIntervalâŸ©) =
    Î³ n t := by
  rw [countableConcat, coe_mk_mk]
  refine (countableConcatFun_eqOn Î³ n âŸ¨?_, ?_âŸ©).trans ?_
  Â· rw [symm_le_symm, Subtype.mk_le_mk, â† one_div]
    exact div_le_div_of_nonneg_right (by linarith [t.2.1]) (by simp)
  Â· rw [symm_le_symm, Subtype.mk_le_mk, pow_succ', â† one_div, â† div_div]
    exact div_le_div_of_nonneg_right (by linarith [t.2.2]) (by simp)
  Â· simp [mul_div_cancelâ‚€ t.1 two_pos.ne']

/-- The concatenation of a sequence of paths is the same as the concatenation of the first path
with the concatenation of the remaining paths. -/
lemma countableConcat_eq_trans {Î³ : (n : â„•) â†’ Path (s n) (s (n + 1))} {x : X}
    (hÎ³x : Tendsto (fun x : â„• Ã— I â†¦ Î³ x.1 x.2) (atTop Ã—Ë¢ âŠ¤) (ğ“ x)) :
    countableConcat Î³ x hÎ³x = (Î³ 0).trans (countableConcat (fun n â†¦ Î³ (n + 1)) x <|
      hÎ³x.comp ((tendsto_add_atTop_nat 1).prodMap tendsto_id)) := by
  ext t
  by_cases ht : (t : â„) â‰¤ 1 / 2 <;> dsimp [trans, countableConcat] <;> simp only [ht, â†“reduceIte]
  Â· refine (countableConcatFun_eqOn Î³ 0 ?_).trans <| by simp
    simpa [â† Subtype.coe_le_coe, show (1 - 2â»Â¹ : â„) = 2â»Â¹ by ring] using ht
  Â· apply lt_of_not_le at ht
    by_cases ht' : t < 1
    Â· dsimp [extend, IccExtend, countableConcatFun]
      have ht'' : 0 < 1 - t.1 := by linarith [unitInterval.coe_lt_one.2 ht']
      have h : (projIcc 0 1 one_pos.le (2 * t.1 - 1) : â„) = 2 * t - 1 := by
        rw [projIcc_of_mem _ âŸ¨by linarith, by linarithâŸ©]
      simp only [ht', â†“reduceDIte, â† Subtype.coe_lt_coe, h, Icc.coe_one,
        show 2 * t.1 - 1 < 1 by linarith]
      refine congr (congrArg (fun n â†¦ â‡‘(Î³ n)) ?_) ?_
      Â· rw [h, â† sub_add, â† add_sub_right_comm, one_add_one_eq_two, â† mul_one_sub,
          mul_inv, â† div_eq_inv_mul, Nat.floor_div_ofNat, Nat.log_div_base]
        refine (Nat.sub_one_add_one (Nat.log_pos one_lt_two ?_).ne').symm
        rw [Nat.le_floor_iff (inv_pos.2 ht'').le]
        exact le_inv_of_le_invâ‚€ ht'' <| by linarith
      Â· rw [Subtype.mk_eq_mk, â† sub_add, â† add_sub_right_comm, one_add_one_eq_two, â† mul_one_sub,
          mul_inv, â† div_eq_inv_mul]
        rw [Nat.floor_div_ofNat, Nat.log_div_base]
        simp_rw [Nat.cast_pow]; rw [pow_subâ‚€ _ two_pos.ne' ?_]
        Â· ring
        Â· rw [â† Nat.pow_le_iff_le_log one_lt_two <| (Nat.floor_pos.2 <| (one_le_invâ‚€ ht'').2
            (by exact (Ïƒ t).2.2)).ne', Nat.le_floor_iff (inv_pos.2 ht'').le]
          exact le_inv_of_le_invâ‚€ ht'' <| by linarith
    Â· rw [show t = 1 by simpa [unitInterval.lt_one_iff_ne_one] using ht']
      simp [show (2 - 1 : â„) = 1 by ring, countableConcatFun]

/-- The image of a countable concatenation of paths is the concatenation of the images. -/
lemma map_countableConcat {Î³ : (n : â„•) â†’ Path (s n) (s (n + 1))} {x : X}
    {hÎ³x : Tendsto (fun x : â„• Ã— I â†¦ Î³ x.1 x.2) (atTop Ã—Ë¢ âŠ¤) (ğ“ x)} {f : X â†’ Y} (hf : Continuous f) :
    (countableConcat Î³ x hÎ³x).map hf =
      countableConcat (fun n â†¦ (Î³ n).map hf) (f x) ((hf.tendsto x).comp hÎ³x) := by
  ext t
  by_cases ht : t < 1 <;> simp [countableConcat, countableConcatFun, ht]

end Path
