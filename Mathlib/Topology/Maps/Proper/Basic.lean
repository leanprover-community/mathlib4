/-
Copyright (c) 2023 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker, Etienne Marion
-/
import Mathlib.Topology.Homeomorph.Lemmas
import Mathlib.Topology.Filter

/-!
# Proper maps between topological spaces

This file develops the basic theory of proper maps between topological spaces. A map `f : X â†’ Y`
between two topological spaces is said to be **proper** if it is continuous and satisfies
the following equivalent conditions:
1. `f` is closed and has compact fibers.
2. `f` is **universally closed**, in the sense that for any topological space `Z`, the map
  `Prod.map f id : X Ã— Z â†’ Y Ã— Z` is closed.
3. For any `â„± : Filter X`, all cluster points of `map f â„±` are images by `f` of some cluster point
  of `â„±`.

We take 3 as the definition in `IsProperMap`, and we show the equivalence with 1, 2, and some
other variations.

## Main statements

* `isProperMap_iff_ultrafilter`: characterization of proper maps in terms of limits of ultrafilters
  instead of cluster points of filters.
* `IsProperMap.pi_map`: any product of proper maps is proper.
* `isProperMap_iff_isClosedMap_and_compact_fibers`: a map is proper if and only if it is
  continuous, closed, and has compact fibers

## Implementation notes

In algebraic geometry, it is common to also ask that proper maps are *separated*, in the sense of
[Stacks: definition OCY1](https://stacks.math.columbia.edu/tag/0CY1). We don't follow this
convention because it is unclear whether it would give the right notion in all cases, and in
particular for the theory of proper group actions. That means that our terminology does **NOT**
align with that of [Stacks: Characterizing proper maps](https://stacks.math.columbia.edu/tag/005M),
instead our definition of `IsProperMap` coincides with what they call "Bourbaki-proper".

Regarding the proofs, we don't really follow Bourbaki and go for more filter-heavy proofs,
as usual. In particular, their arguments rely heavily on restriction of closed maps (see
`IsClosedMap.restrictPreimage`), which makes them somehow annoying to formalize in type theory.
In contrast, the filter-based proofs work really well thanks to the existing API.

In fact, the filter proofs work so well that I thought this would be a great pedagogical resource
about how we use filters. For that reason, **all interesting proofs in this file are commented**,
so don't hesitate to have a look!

## TODO

* prove the equivalence with condition 3 of
  [Stacks: Theorem 005R](https://stacks.math.columbia.edu/tag/005R). Note that they mean something
  different by "universally closed".

## References

* [N. Bourbaki, *General Topology*][bourbaki1966]
* [Stacks: Characterizing proper maps](https://stacks.math.columbia.edu/tag/005M)
-/

assert_not_exists StoneCech

open Filter Topology Function Set
open Prod (fst snd)

variable {X Y Z W Î¹ : Type*} [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]
  [TopologicalSpace W] {f : X â†’ Y} {g : Y â†’ Z}

universe u v

/-- A map `f : X â†’ Y` between two topological spaces is said to be **proper** if it is continuous
and, for all `â„± : Filter X`, any cluster point of `map f â„±` is the image by `f` of a cluster point
of `â„±`. -/
@[mk_iff isProperMap_iff_clusterPt, fun_prop]
structure IsProperMap (f : X â†’ Y) : Prop extends Continuous f where
  /-- By definition, if `f` is a proper map and `â„±` is any filter on `X`, then any cluster point of
  `map f â„±` is the image by `f` of some cluster point of `â„±`. -/
  clusterPt_of_mapClusterPt :
    âˆ€ â¦ƒâ„± : Filter Xâ¦„, âˆ€ â¦ƒy : Yâ¦„, MapClusterPt y â„± f â†’ âˆƒ x, f x = y âˆ§ ClusterPt x â„±

/-- Definition of proper maps. See also `isClosedMap_iff_clusterPt` for a related criterion
for closed maps. -/
add_decl_doc isProperMap_iff_clusterPt

/-- By definition, a proper map is continuous. -/
@[fun_prop]
lemma IsProperMap.continuous (h : IsProperMap f) : Continuous f := h.toContinuous

/-- A proper map is closed. -/
lemma IsProperMap.isClosedMap (h : IsProperMap f) : IsClosedMap f := by
  rw [isClosedMap_iff_clusterPt]
  exact fun s y â†¦ h.clusterPt_of_mapClusterPt (â„± := ğ“Ÿ s) (y := y)

/-- Characterization of proper maps by ultrafilters. -/
lemma isProperMap_iff_ultrafilter : IsProperMap f â†” Continuous f âˆ§
    âˆ€ â¦ƒğ’° : Ultrafilter Xâ¦„, âˆ€ â¦ƒy : Yâ¦„, Tendsto f ğ’° (ğ“ y) â†’ âˆƒ x, f x = y âˆ§ ğ’° â‰¤ ğ“ x := by
  -- This is morally trivial since ultrafilters give all the information about cluster points.
  rw [isProperMap_iff_clusterPt]
  refine and_congr_right (fun _ â†¦ ?_)
  constructor <;> intro H
  Â· intro ğ’° y (hY : (Ultrafilter.map f ğ’° : Filter Y) â‰¤ _)
    simp_rw [â† Ultrafilter.clusterPt_iff] at hY âŠ¢
    exact H hY
  Â· simp_rw [MapClusterPt, ClusterPt, â† Filter.push_pull', map_neBot_iff, â† exists_ultrafilter_iff,
      forall_exists_index]
    intro â„± y ğ’° hy
    rcases H (tendsto_iff_comap.mpr <| hy.trans inf_le_left) with âŸ¨x, hxy, hxâŸ©
    exact âŸ¨x, hxy, ğ’°, le_inf hx (hy.trans inf_le_right)âŸ©

lemma isProperMap_iff_ultrafilter_of_t2 [T2Space Y] : IsProperMap f â†” Continuous f âˆ§
    âˆ€ â¦ƒğ’° : Ultrafilter Xâ¦„, âˆ€ â¦ƒy : Yâ¦„, Tendsto f ğ’° (ğ“ y) â†’ âˆƒ x, ğ’°.1 â‰¤ ğ“ x :=
  isProperMap_iff_ultrafilter.trans <| and_congr_right fun hc â†¦ forallâ‚ƒ_congr fun _ğ’° _y hy â†¦
    exists_congr fun x â†¦ and_iff_right_of_imp fun h â†¦
      tendsto_nhds_unique ((hc.tendsto x).mono_left h) hy

/-- If `f` is proper and converges to `y` along some ultrafilter `ğ’°`, then `ğ’°` converges to some
`x` such that `f x = y`. -/
lemma IsProperMap.ultrafilter_le_nhds_of_tendsto (h : IsProperMap f) â¦ƒğ’° : Ultrafilter Xâ¦„ â¦ƒy : Yâ¦„
    (hy : Tendsto f ğ’° (ğ“ y)) : âˆƒ x, f x = y âˆ§ ğ’° â‰¤ ğ“ x :=
  (isProperMap_iff_ultrafilter.mp h).2 hy

/-- The composition of two proper maps is proper. -/
lemma IsProperMap.comp (hg : IsProperMap g) (hf : IsProperMap f) :
    IsProperMap (g âˆ˜ f) := by
  refine âŸ¨by fun_prop, fun â„± z h â†¦ ?_âŸ©
  rw [mapClusterPt_comp] at h
  rcases hg.clusterPt_of_mapClusterPt h with âŸ¨y, rfl, hyâŸ©
  rcases hf.clusterPt_of_mapClusterPt hy with âŸ¨x, rfl, hxâŸ©
  use x, rfl

/-- If the composition of two continuous functions `g âˆ˜ f` is proper and `f` is surjective,
then `g` is proper. -/
lemma isProperMap_of_comp_of_surj (hf : Continuous f)
    (hg : Continuous g) (hgf : IsProperMap (g âˆ˜ f)) (f_surj : f.Surjective) : IsProperMap g := by
  refine âŸ¨hg, fun â„± z h â†¦ ?_âŸ©
  rw [â† â„±.map_comap_of_surjective f_surj, â† mapClusterPt_comp] at h
  rcases hgf.clusterPt_of_mapClusterPt h with âŸ¨x, rfl, hxâŸ©
  rw [â† â„±.map_comap_of_surjective f_surj]
  exact âŸ¨f x, rfl, hx.map hf.continuousAt tendsto_mapâŸ©

/-- If the composition of two continuous functions `g âˆ˜ f` is proper and `g` is injective,
then `f` is proper. -/
lemma isProperMap_of_comp_of_inj {f : X â†’ Y} {g : Y â†’ Z} (hf : Continuous f) (hg : Continuous g)
    (hgf : IsProperMap (g âˆ˜ f)) (g_inj : g.Injective) : IsProperMap f := by
  refine âŸ¨hf, fun â„± y h â†¦ ?_âŸ©
  rcases hgf.clusterPt_of_mapClusterPt (h.map hg.continuousAt tendsto_map) with âŸ¨x, hx1, hx2âŸ©
  exact âŸ¨x, g_inj hx1, hx2âŸ©

/-- If the composition of two continuous functions `f : X â†’ Y` and `g : Y â†’ Z` is proper
and `Y` is T2, then `f` is proper. -/
lemma isProperMap_of_comp_of_t2 [T2Space Y] (hf : Continuous f) (hg : Continuous g)
    (hgf : IsProperMap (g âˆ˜ f)) : IsProperMap f := by
  rw [isProperMap_iff_ultrafilter_of_t2]
  refine âŸ¨hf, fun ğ’° y h â†¦ ?_âŸ©
  rw [isProperMap_iff_ultrafilter] at hgf
  rcases hgf.2 ((hg.tendsto y).comp h) with âŸ¨x, -, hxâŸ©
  exact âŸ¨x, hxâŸ©

/-- A binary product of proper maps is proper. -/
lemma IsProperMap.prodMap {g : Z â†’ W} (hf : IsProperMap f) (hg : IsProperMap g) :
    IsProperMap (Prod.map f g) := by
  simp_rw [isProperMap_iff_ultrafilter] at hf hg âŠ¢
  constructor
  -- Continuity is clear.
  Â· exact hf.1.prodMap hg.1
  -- Let `ğ’° : Ultrafilter (X Ã— Z)`, and assume that `f Ã— g` tends to some `(y, w) : Y Ã— W`
  -- along `ğ’°`.
  Â· intro ğ’° âŸ¨y, wâŸ© hyw
  -- That means that `f` tends to `y` along `map fst ğ’°` and `g` tends to `w` along `map snd ğ’°`.
    simp_rw [nhds_prod_eq, tendsto_prod_iff'] at hyw
  -- Thus, by properness of `f` and `g`, we get some `x : X` and `z : Z` such that `f x = y`,
  -- `g z = w`, `map fst ğ’°` tends to  `x`, and `map snd ğ’°` tends to `y`.
    rcases hf.2 (show Tendsto f (Ultrafilter.map fst ğ’°) (ğ“ y) by simpa using hyw.1) with
      âŸ¨x, hxy, hxâŸ©
    rcases hg.2 (show Tendsto g (Ultrafilter.map snd ğ’°) (ğ“ w) by simpa using hyw.2) with
      âŸ¨z, hzw, hzâŸ©
  -- By the properties of the product topology, that means that `ğ’°` tends to `(x, z)`,
  -- which completes the proof since `(f Ã— g)(x, z) = (y, w)`.
    refine âŸ¨âŸ¨x, zâŸ©, Prod.ext hxy hzw, ?_âŸ©
    rw [nhds_prod_eq, le_prod]
    exact âŸ¨hx, hzâŸ©

/-- Any product of proper maps is proper. -/
lemma IsProperMap.pi_map {X Y : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (X i)]
    [âˆ€ i, TopologicalSpace (Y i)] {f : (i : Î¹) â†’ X i â†’ Y i} (h : âˆ€ i, IsProperMap (f i)) :
    IsProperMap (fun (x : âˆ€ i, X i) i â†¦ f i (x i)) := by
  simp_rw [isProperMap_iff_ultrafilter] at h âŠ¢
  constructor
  -- Continuity is clear.
  Â· exact continuous_pi fun i â†¦ (h i).1.comp (continuous_apply i)
  -- Let `ğ’° : Ultrafilter (Î  i, X i)`, and assume that `Î  i, f i` tends to some `y : Î  i, Y i`
  -- along `ğ’°`.
  Â· intro ğ’° y hy
  -- That means that each `f i` tends to `y i` along `map (eval i) ğ’°`.
    have : âˆ€ i, Tendsto (f i) (Ultrafilter.map (eval i) ğ’°) (ğ“ (y i)) := by
      simpa [tendsto_pi_nhds] using hy
  -- Thus, by properness of all the `f i`s, we can choose some `x : Î  i, X i` such that, for all
  -- `i`, `f i (x i) = y i` and `map (eval i) ğ’°` tends to  `x i`.
    choose x hxy hx using fun i â†¦ (h i).2 (this i)
  -- By the properties of the product topology, that means that `ğ’°` tends to `x`,
  -- which completes the proof since `(Î  i, f i) x = y`.
    refine âŸ¨x, funext hxy, ?_âŸ©
    rwa [nhds_pi, le_pi]

/-- The preimage of a compact set by a proper map is again compact. See also
`isProperMap_iff_isCompact_preimage` which proves that this property completely characterizes
proper map when the codomain is compactly generated and Hausdorff. -/
lemma IsProperMap.isCompact_preimage (h : IsProperMap f) {K : Set Y} (hK : IsCompact K) :
    IsCompact (f â»Â¹' K) := by
  rw [isCompact_iff_ultrafilter_le_nhds]
  -- Let `ğ’° â‰¤ ğ“Ÿ (f â»Â¹' K)` an ultrafilter.
  intro ğ’° hğ’°
  -- In other words, we have `map f ğ’° â‰¤ ğ“Ÿ K`
  rw [â† comap_principal, â† map_le_iff_le_comap, â† Ultrafilter.coe_map] at hğ’°
  -- Thus, by compactness of `K`, the ultrafilter `map f ğ’°` tends to some `y âˆˆ K`.
  rcases hK.ultrafilter_le_nhds _ hğ’° with âŸ¨y, hyK, hyâŸ©
  -- Then, by properness of `f`, that means that `ğ’°` tends to some `x âˆˆ f â»Â¹' {y} âŠ† f â»Â¹' K`,
  -- which completes the proof.
  rcases h.ultrafilter_le_nhds_of_tendsto hy with âŸ¨x, rfl, hxâŸ©
  exact âŸ¨x, hyK, hxâŸ©

/-- A map is proper if and only if it is closed and its fibers are compact. -/
theorem isProperMap_iff_isClosedMap_and_compact_fibers :
    IsProperMap f â†” Continuous f âˆ§ IsClosedMap f âˆ§ âˆ€ y, IsCompact (f â»Â¹' {y}) := by
  constructor <;> intro H
  -- Note: In Bourbaki, the direct implication is proved by going through universally closed maps.
  -- We could do the same (using a `TFAE` cycle) but proving it directly from
  -- `IsProperMap.isCompact_preimage` is nice enough already so we don't bother with that.
  Â· exact âŸ¨H.continuous, H.isClosedMap, fun y â†¦ H.isCompact_preimage isCompact_singletonâŸ©
  Â· rw [isProperMap_iff_clusterPt]
  -- Let `â„± : Filter X` and `y` some cluster point of `map f â„±`.
    refine âŸ¨H.1, fun â„± y hy â†¦ ?_âŸ©
  -- That means that the singleton `pure y` meets the "closure" of `map f â„±`, by which we mean
  -- `Filter.lift' (map f â„±) closure`. But `f` is closed, so
  -- `closure (map f â„±) = map f (closure â„±)` (see `IsClosedMap.lift'_closure_map_eq`).
  -- Thus `map f (closure â„± âŠ“ ğ“Ÿ (f â»Â¹' {y})) = map f (closure â„±) âŠ“ ğ“Ÿ {y} â‰  âŠ¥`, hence
  -- `closure â„± âŠ“ ğ“Ÿ (f â»Â¹' {y}) â‰  âŠ¥`.
    rw [H.2.1.mapClusterPt_iff_lift'_closure H.1] at hy
  -- Now, applying the compactness of `f â»Â¹' {y}` to the nontrivial filter
  -- `closure â„± âŠ“ ğ“Ÿ (f â»Â¹' {y})`, we obtain that it has a cluster point `x âˆˆ f â»Â¹' {y}`.
    rcases H.2.2 y (f := Filter.lift' â„± closure âŠ“ ğ“Ÿ (f â»Â¹' {y})) inf_le_right with âŸ¨x, hxy, hxâŸ©
    refine âŸ¨x, hxy, ?_âŸ©
  -- In particular `x` is a cluster point of `closure â„±`. Since cluster points of `closure â„±`
  -- are exactly cluster points of `â„±` (see `clusterPt_lift'_closure_iff`), this completes
  -- the proof.
    rw [â† clusterPt_lift'_closure_iff]
    exact hx.mono inf_le_left

/-- An injective and continuous function is proper if and only if it is closed. -/
lemma isProperMap_iff_isClosedMap_of_inj (f_cont : Continuous f) (f_inj : f.Injective) :
    IsProperMap f â†” IsClosedMap f := by
  refine âŸ¨fun h â†¦ h.isClosedMap, fun h â†¦ ?_âŸ©
  rw [isProperMap_iff_isClosedMap_and_compact_fibers]
  exact âŸ¨f_cont, h, fun y â†¦ (subsingleton_singleton.preimage f_inj).isCompactâŸ©

/-- A injective continuous and closed map is proper. -/
lemma isProperMap_of_isClosedMap_of_inj (f_cont : Continuous f) (f_inj : f.Injective)
    (f_closed : IsClosedMap f) : IsProperMap f :=
  (isProperMap_iff_isClosedMap_of_inj f_cont f_inj).2 f_closed

/-- A homeomorphism is proper. -/
@[simp] lemma Homeomorph.isProperMap (e : X â‰ƒâ‚œ Y) : IsProperMap e :=
  isProperMap_of_isClosedMap_of_inj e.continuous e.injective e.isClosedMap

protected lemma IsHomeomorph.isProperMap (hf : IsHomeomorph f) : IsProperMap f :=
  isProperMap_of_isClosedMap_of_inj hf.continuous hf.injective hf.isClosedMap

/-- The identity is proper. -/
@[simp] lemma isProperMap_id : IsProperMap (id : X â†’ X) := IsHomeomorph.id.isProperMap

/-- A closed embedding is proper. -/
lemma Topology.IsClosedEmbedding.isProperMap (hf : IsClosedEmbedding f) : IsProperMap f :=
  isProperMap_of_isClosedMap_of_inj hf.continuous hf.injective hf.isClosedMap

/-- The coercion from a closed subset is proper. -/
lemma IsClosed.isProperMap_subtypeVal {C : Set X} (hC : IsClosed C) : IsProperMap ((â†‘) : C â†’ X) :=
  hC.isClosedEmbedding_subtypeVal.isProperMap

/-- The restriction of a proper map to a closed subset is proper. -/
lemma IsProperMap.restrict {C : Set X} (hf : IsProperMap f) (hC : IsClosed C) :
    IsProperMap fun x : C â†¦ f x := hf.comp hC.isProperMap_subtypeVal

/-- The range of a proper map is closed. -/
lemma IsProperMap.isClosed_range (hf : IsProperMap f) : IsClosed (range f) :=
  hf.isClosedMap.isClosed_range

/-- Version of `isProperMap_iff_isClosedMap_and_compact_fibers` in terms of `cofinite` and
`cocompact`. Only works when the codomain is `T1`. -/
lemma isProperMap_iff_isClosedMap_and_tendsto_cofinite [T1Space Y] :
    IsProperMap f â†” Continuous f âˆ§ IsClosedMap f âˆ§ Tendsto f (cocompact X) cofinite := by
  simp_rw [isProperMap_iff_isClosedMap_and_compact_fibers, Tendsto,
    le_cofinite_iff_compl_singleton_mem, mem_map, preimage_compl]
  refine and_congr_right fun f_cont â†¦ and_congr_right fun _ â†¦
    âŸ¨fun H y â†¦ (H y).compl_mem_cocompact, fun H y â†¦ ?_âŸ©
  rcases mem_cocompact.mp (H y) with âŸ¨K, hK, hKyâŸ©
  exact hK.of_isClosed_subset (isClosed_singleton.preimage f_cont)
    (compl_le_compl_iff_le.mp hKy)

/-- A continuous map from a compact space to a Tâ‚‚ space is a proper map. -/
theorem Continuous.isProperMap [CompactSpace X] [T2Space Y] (hf : Continuous f) : IsProperMap f :=
  isProperMap_iff_isClosedMap_and_tendsto_cofinite.2 âŸ¨hf, hf.isClosedMap, by simpâŸ©

/-- A proper map `f : X â†’ Y` is **universally closed**: for any topological space `Z`, the map
`Prod.map f id : X Ã— Z â†’ Y Ã— Z` is closed. We will prove in `isProperMap_iff_universally_closed`
that proper maps are exactly continuous maps which have this property, but this result should be
easier to use because it allows `Z` to live in any universe. -/
theorem IsProperMap.universally_closed (Z) [TopologicalSpace Z] (h : IsProperMap f) :
    IsClosedMap (Prod.map f id : X Ã— Z â†’ Y Ã— Z) :=
  -- `f Ã— id` is proper as a product of proper maps, hence closed.
  (h.prodMap isProperMap_id).isClosedMap

/-- A map `f : X â†’ Y` is proper if and only if it is continuous and the map
`(Prod.map f id : X Ã— Filter X â†’ Y Ã— Filter X)` is closed. This is stronger than
`isProperMap_iff_universally_closed` since it shows that there's only one space to check to get
properness, but in most cases it doesn't matter. -/
theorem isProperMap_iff_isClosedMap_filter {X : Type u} {Y : Type v} [TopologicalSpace X]
    [TopologicalSpace Y] {f : X â†’ Y} :
    IsProperMap f â†” Continuous f âˆ§ IsClosedMap
      (Prod.map f id : X Ã— Filter X â†’ Y Ã— Filter X) := by
  constructor <;> intro H
  -- The direct implication is clear.
  Â· exact âŸ¨H.continuous, H.universally_closed _âŸ©
  Â· rw [isProperMap_iff_ultrafilter]
  -- Let `ğ’° : Ultrafilter X`, and assume that `f` tends to some `y` along `ğ’°`.
    refine âŸ¨H.1, fun ğ’° y hy â†¦ ?_âŸ©
  -- In `X Ã— Filter X`, consider the closed set `F := closure {(x, â„±) | â„± = pure x}`
    let F : Set (X Ã— Filter X) := closure {xâ„± | xâ„±.2 = pure xâ„±.1}
  -- Since `f Ã— id` is closed, the set `(f Ã— id) '' F` is also closed.
    have := H.2 F isClosed_closure
  -- Let us show that `(y, ğ’°) âˆˆ (f Ã— id) '' F`.
    have : (y, â†‘ğ’°) âˆˆ Prod.map f id '' F :=
  -- Note that, by the properties of the topology on `Filter X`, the function `pure : X â†’ Filter X`
  -- tends to the point `ğ’°` of `Filter X` along the filter `ğ’°` on `X`. Since `f` tends to `y` along
  -- `ğ’°`, we get that the function `(f, pure) : X â†’ (Y, Filter X)` tends to `(y, ğ’°)` along
  -- `ğ’°`. Furthermore, each `(f, pure)(x) = (f Ã— id)(x, pure x)` is clearly an element of
  -- the closed set `(f Ã— id) '' F`, thus the limit `(y, ğ’°)` also belongs to that set.
      this.mem_of_tendsto (hy.prodMk_nhds (Filter.tendsto_pure_self (ğ’° : Filter X)))
        (Eventually.of_forall fun x â†¦ âŸ¨âŸ¨x, pure xâŸ©, subset_closure rfl, rflâŸ©)
  -- The above shows that `(y, ğ’°) = (f x, ğ’°)`, for some `x : X` such that `(x, ğ’°) âˆˆ F`.
    rcases this with âŸ¨âŸ¨x, _âŸ©, hx, âŸ¨_, _âŸ©âŸ©
  -- We already know that `f x = y`, so to finish the proof we just have to check that `ğ’°` tends
  -- to `x`. So, for `U âˆˆ ğ“ x` arbitrary, let's show that `U âˆˆ ğ’°`. Since `ğ’°` is a ultrafilter,
  -- it is enough to show that `Uá¶œ` is not in `ğ’°`.
    refine âŸ¨x, rfl, fun U hU â†¦ Ultrafilter.compl_notMem_iff.mp fun hUc â†¦ ?_âŸ©
    rw [mem_closure_iff_nhds] at hx
  -- Indeed, if that was the case, the set `V := {ğ’¢ : Filter X | Uá¶œ âˆˆ ğ’¢}` would be a neighborhood
  -- of `ğ’°` in `Filter X`, hence `U Ã—Ë¢ V` would be a neighborhood of `(x, ğ’°) : X Ã— Filter X`.
  -- But recall that `(x, ğ’°) âˆˆ F = closure {(x, â„±) | â„± = pure x}`, so the neighborhood `U Ã—Ë¢ V`
  -- must contain some element of the form `(z, pure z)`. In other words, we have `z âˆˆ U` and
  -- `Uá¶œ âˆˆ pure z`, which means `z âˆˆ Uá¶œ` by the definition of pure.
  -- This is a contradiction, which completes the proof.
    rcases hx (U Ã—Ë¢ {ğ’¢ | Uá¶œ âˆˆ ğ’¢}) (prod_mem_nhds hU (isOpen_setOf_mem.mem_nhds hUc)) with
      âŸ¨âŸ¨z, ğ’¢âŸ©, âŸ¨âŸ¨hz : z âˆˆ U, hz' : Uá¶œ âˆˆ ğ’¢âŸ©, rfl : ğ’¢ = pure zâŸ©âŸ©
    exact hz' hz
