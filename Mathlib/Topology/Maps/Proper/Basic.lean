/-
Copyright (c) 2023 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker, Etienne Marion
-/
import Mathlib.Topology.Homeomorph.Lemmas

/-!
# Proper maps between topological spaces

This file develops the basic theory of proper maps between topological spaces. A map `f : X ‚Üí Y`
between two topological spaces is said to be **proper** if it is continuous and satisfies
the following equivalent conditions:
1. `f` is closed and has compact fibers.
2. `f` is **universally closed**, in the sense that for any topological space `Z`, the map
  `Prod.map f id : X √ó Z ‚Üí Y √ó Z` is closed.
3. For any `‚Ñ± : Filter X`, all cluster points of `map f ‚Ñ±` are images by `f` of some cluster point
  of `‚Ñ±`.

We take 3 as the definition in `IsProperMap`, and we show the equivalence with 1, 2, and some
other variations.

## Main statements

* `isProperMap_iff_ultrafilter`: characterization of proper maps in terms of limits of ultrafilters
  instead of cluster points of filters.
* `IsProperMap.pi_map`: any product of proper maps is proper.
* `isProperMap_iff_isClosedMap_and_compact_fibers`: a map is proper if and only if it is
  continuous, closed, and has compact fibers

## Implementation notes

In algebraic geometry, it is common to also ask that proper maps are *separated*, in the sense of
[Stacks: definition OCY1](https://stacks.math.columbia.edu/tag/0CY1). We don't follow this
convention because it is unclear whether it would give the right notion in all cases, and in
particular for the theory of proper group actions. That means that our terminology does **NOT**
align with that of [Stacks: Characterizing proper maps](https://stacks.math.columbia.edu/tag/005M),
instead our definition of `IsProperMap` coincides with what they call "Bourbaki-proper".

Regarding the proofs, we don't really follow Bourbaki and go for more filter-heavy proofs,
as usual. In particular, their arguments rely heavily on restriction of closed maps (see
`IsClosedMap.restrictPreimage`), which makes them somehow annoying to formalize in type theory.
In contrast, the filter-based proofs work really well thanks to the existing API.

In fact, the filter proofs work so well that I thought this would be a great pedagogical resource
about how we use filters. For that reason, **all interesting proofs in this file are commented**,
so don't hesitate to have a look!

## TODO

* prove the equivalence with condition 3 of
  [Stacks: Theorem 005R](https://stacks.math.columbia.edu/tag/005R). Note that they mean something
  different by "universally closed".

## References

* [N. Bourbaki, *General Topology*][bourbaki1966]
* [Stacks: Characterizing proper maps](https://stacks.math.columbia.edu/tag/005M)
-/

assert_not_exists StoneCech

open Filter Topology Function Set
open Prod (fst snd)

variable {X Y Z W Œπ : Type*} [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]
  [TopologicalSpace W] {f : X ‚Üí Y} {g : Y ‚Üí Z}

/-- A map `f : X ‚Üí Y` between two topological spaces is said to be **proper** if it is continuous
and, for all `‚Ñ± : Filter X`, any cluster point of `map f ‚Ñ±` is the image by `f` of a cluster point
of `‚Ñ±`. -/
@[mk_iff isProperMap_iff_clusterPt, fun_prop]
structure IsProperMap (f : X ‚Üí Y) : Prop extends Continuous f where
  /-- By definition, if `f` is a proper map and `‚Ñ±` is any filter on `X`, then any cluster point of
  `map f ‚Ñ±` is the image by `f` of some cluster point of `‚Ñ±`. -/
  clusterPt_of_mapClusterPt :
    ‚àÄ ‚¶É‚Ñ± : Filter X‚¶Ñ, ‚àÄ ‚¶Éy : Y‚¶Ñ, MapClusterPt y ‚Ñ± f ‚Üí ‚àÉ x, f x = y ‚àß ClusterPt x ‚Ñ±

/-- Definition of proper maps. See also `isClosedMap_iff_clusterPt` for a related criterion
for closed maps. -/
add_decl_doc isProperMap_iff_clusterPt

/-- By definition, a proper map is continuous. -/
@[fun_prop]
lemma IsProperMap.continuous (h : IsProperMap f) : Continuous f := h.toContinuous

/-- A proper map is closed. -/
lemma IsProperMap.isClosedMap (h : IsProperMap f) : IsClosedMap f := by
  rw [isClosedMap_iff_clusterPt]
  exact fun s y ‚Ü¶ h.clusterPt_of_mapClusterPt (‚Ñ± := ùìü s) (y := y)

/-- Characterization of proper maps by ultrafilters. -/
lemma isProperMap_iff_ultrafilter : IsProperMap f ‚Üî Continuous f ‚àß
    ‚àÄ ‚¶Éùí∞ : Ultrafilter X‚¶Ñ, ‚àÄ ‚¶Éy : Y‚¶Ñ, Tendsto f ùí∞ (ùìù y) ‚Üí ‚àÉ x, f x = y ‚àß ùí∞ ‚â§ ùìù x := by
  -- This is morally trivial since ultrafilters give all the information about cluster points.
  rw [isProperMap_iff_clusterPt]
  refine and_congr_right (fun _ ‚Ü¶ ?_)
  constructor <;> intro H
  ¬∑ intro ùí∞ y (hY : (Ultrafilter.map f ùí∞ : Filter Y) ‚â§ _)
    simp_rw [‚Üê Ultrafilter.clusterPt_iff] at hY ‚ä¢
    exact H hY
  ¬∑ simp_rw [MapClusterPt, ClusterPt, ‚Üê Filter.push_pull', map_neBot_iff, ‚Üê exists_ultrafilter_iff,
      forall_exists_index]
    intro ‚Ñ± y ùí∞ hy
    rcases H (tendsto_iff_comap.mpr <| hy.trans inf_le_left) with ‚ü®x, hxy, hx‚ü©
    exact ‚ü®x, hxy, ùí∞, le_inf hx (hy.trans inf_le_right)‚ü©

lemma isProperMap_iff_ultrafilter_of_t2 [T2Space Y] : IsProperMap f ‚Üî Continuous f ‚àß
    ‚àÄ ‚¶Éùí∞ : Ultrafilter X‚¶Ñ, ‚àÄ ‚¶Éy : Y‚¶Ñ, Tendsto f ùí∞ (ùìù y) ‚Üí ‚àÉ x, ùí∞.1 ‚â§ ùìù x :=
  isProperMap_iff_ultrafilter.trans <| and_congr_right fun hc ‚Ü¶ forall‚ÇÉ_congr fun _ùí∞ _y hy ‚Ü¶
    exists_congr fun x ‚Ü¶ and_iff_right_of_imp fun h ‚Ü¶
      tendsto_nhds_unique ((hc.tendsto x).mono_left h) hy

/-- If `f` is proper and converges to `y` along some ultrafilter `ùí∞`, then `ùí∞` converges to some
`x` such that `f x = y`. -/
lemma IsProperMap.ultrafilter_le_nhds_of_tendsto (h : IsProperMap f) ‚¶Éùí∞ : Ultrafilter X‚¶Ñ ‚¶Éy : Y‚¶Ñ
    (hy : Tendsto f ùí∞ (ùìù y)) : ‚àÉ x, f x = y ‚àß ùí∞ ‚â§ ùìù x :=
  (isProperMap_iff_ultrafilter.mp h).2 hy

/-- The composition of two proper maps is proper. -/
lemma IsProperMap.comp (hg : IsProperMap g) (hf : IsProperMap f) :
    IsProperMap (g ‚àò f) := by
  refine ‚ü®by fun_prop, fun ‚Ñ± z h ‚Ü¶ ?_‚ü©
  rw [mapClusterPt_comp] at h
  rcases hg.clusterPt_of_mapClusterPt h with ‚ü®y, rfl, hy‚ü©
  rcases hf.clusterPt_of_mapClusterPt hy with ‚ü®x, rfl, hx‚ü©
  use x, rfl

/-- If the composition of two continuous functions `g ‚àò f` is proper and `f` is surjective,
then `g` is proper. -/
lemma isProperMap_of_comp_of_surj (hf : Continuous f)
    (hg : Continuous g) (hgf : IsProperMap (g ‚àò f)) (f_surj : f.Surjective) : IsProperMap g := by
  refine ‚ü®hg, fun ‚Ñ± z h ‚Ü¶ ?_‚ü©
  rw [‚Üê ‚Ñ±.map_comap_of_surjective f_surj, ‚Üê mapClusterPt_comp] at h
  rcases hgf.clusterPt_of_mapClusterPt h with ‚ü®x, rfl, hx‚ü©
  rw [‚Üê ‚Ñ±.map_comap_of_surjective f_surj]
  exact ‚ü®f x, rfl, hx.map hf.continuousAt tendsto_map‚ü©

/-- If the composition of two continuous functions `g ‚àò f` is proper and `g` is injective,
then `f` is proper. -/
lemma isProperMap_of_comp_of_inj {f : X ‚Üí Y} {g : Y ‚Üí Z} (hf : Continuous f) (hg : Continuous g)
    (hgf : IsProperMap (g ‚àò f)) (g_inj : g.Injective) : IsProperMap f := by
  refine ‚ü®hf, fun ‚Ñ± y h ‚Ü¶ ?_‚ü©
  rcases hgf.clusterPt_of_mapClusterPt (h.map hg.continuousAt tendsto_map) with ‚ü®x, hx1, hx2‚ü©
  exact ‚ü®x, g_inj hx1, hx2‚ü©

/-- If the composition of two continuous functions `f : X ‚Üí Y` and `g : Y ‚Üí Z` is proper
and `Y` is T2, then `f` is proper. -/
lemma isProperMap_of_comp_of_t2 [T2Space Y] (hf : Continuous f) (hg : Continuous g)
    (hgf : IsProperMap (g ‚àò f)) : IsProperMap f := by
  rw [isProperMap_iff_ultrafilter_of_t2]
  refine ‚ü®hf, fun ùí∞ y h ‚Ü¶ ?_‚ü©
  rw [isProperMap_iff_ultrafilter] at hgf
  rcases hgf.2 ((hg.tendsto y).comp h) with ‚ü®x, -, hx‚ü©
  exact ‚ü®x, hx‚ü©

/-- A binary product of proper maps is proper. -/
lemma IsProperMap.prodMap {g : Z ‚Üí W} (hf : IsProperMap f) (hg : IsProperMap g) :
    IsProperMap (Prod.map f g) := by
  simp_rw [isProperMap_iff_ultrafilter] at hf hg ‚ä¢
  constructor
  -- Continuity is clear.
  ¬∑ exact hf.1.prodMap hg.1
  -- Let `ùí∞ : Ultrafilter (X √ó Z)`, and assume that `f √ó g` tends to some `(y, w) : Y √ó W`
  -- along `ùí∞`.
  ¬∑ intro ùí∞ ‚ü®y, w‚ü© hyw
  -- That means that `f` tends to `y` along `map fst ùí∞` and `g` tends to `w` along `map snd ùí∞`.
    simp_rw [nhds_prod_eq, tendsto_prod_iff'] at hyw
  -- Thus, by properness of `f` and `g`, we get some `x : X` and `z : Z` such that `f x = y`,
  -- `g z = w`, `map fst ùí∞` tends to  `x`, and `map snd ùí∞` tends to `y`.
    rcases hf.2 (show Tendsto f (Ultrafilter.map fst ùí∞) (ùìù y) by simpa using hyw.1) with
      ‚ü®x, hxy, hx‚ü©
    rcases hg.2 (show Tendsto g (Ultrafilter.map snd ùí∞) (ùìù w) by simpa using hyw.2) with
      ‚ü®z, hzw, hz‚ü©
  -- By the properties of the product topology, that means that `ùí∞` tends to `(x, z)`,
  -- which completes the proof since `(f √ó g)(x, z) = (y, w)`.
    refine ‚ü®‚ü®x, z‚ü©, Prod.ext hxy hzw, ?_‚ü©
    rw [nhds_prod_eq, le_prod]
    exact ‚ü®hx, hz‚ü©

/-- Any product of proper maps is proper. -/
lemma IsProperMap.pi_map {X Y : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (X i)]
    [‚àÄ i, TopologicalSpace (Y i)] {f : (i : Œπ) ‚Üí X i ‚Üí Y i} (h : ‚àÄ i, IsProperMap (f i)) :
    IsProperMap (fun (x : ‚àÄ i, X i) i ‚Ü¶ f i (x i)) := by
  simp_rw [isProperMap_iff_ultrafilter] at h ‚ä¢
  constructor
  -- Continuity is clear.
  ¬∑ exact continuous_pi fun i ‚Ü¶ (h i).1.comp (continuous_apply i)
  -- Let `ùí∞ : Ultrafilter (Œ† i, X i)`, and assume that `Œ† i, f i` tends to some `y : Œ† i, Y i`
  -- along `ùí∞`.
  ¬∑ intro ùí∞ y hy
  -- That means that each `f i` tends to `y i` along `map (eval i) ùí∞`.
    have : ‚àÄ i, Tendsto (f i) (Ultrafilter.map (eval i) ùí∞) (ùìù (y i)) := by
      simpa [tendsto_pi_nhds] using hy
  -- Thus, by properness of all the `f i`s, we can choose some `x : Œ† i, X i` such that, for all
  -- `i`, `f i (x i) = y i` and `map (eval i) ùí∞` tends to  `x i`.
    choose x hxy hx using fun i ‚Ü¶ (h i).2 (this i)
  -- By the properties of the product topology, that means that `ùí∞` tends to `x`,
  -- which completes the proof since `(Œ† i, f i) x = y`.
    refine ‚ü®x, funext hxy, ?_‚ü©
    rwa [nhds_pi, le_pi]

/-- The preimage of a compact set by a proper map is again compact. See also
`isProperMap_iff_isCompact_preimage` which proves that this property completely characterizes
proper map when the codomain is compactly generated and Hausdorff. -/
lemma IsProperMap.isCompact_preimage (h : IsProperMap f) {K : Set Y} (hK : IsCompact K) :
    IsCompact (f ‚Åª¬π' K) := by
  rw [isCompact_iff_ultrafilter_le_nhds]
  -- Let `ùí∞ ‚â§ ùìü (f ‚Åª¬π' K)` an ultrafilter.
  intro ùí∞ hùí∞
  -- In other words, we have `map f ùí∞ ‚â§ ùìü K`
  rw [‚Üê comap_principal, ‚Üê map_le_iff_le_comap, ‚Üê Ultrafilter.coe_map] at hùí∞
  -- Thus, by compactness of `K`, the ultrafilter `map f ùí∞` tends to some `y ‚àà K`.
  rcases hK.ultrafilter_le_nhds _ hùí∞ with ‚ü®y, hyK, hy‚ü©
  -- Then, by properness of `f`, that means that `ùí∞` tends to some `x ‚àà f ‚Åª¬π' {y} ‚äÜ f ‚Åª¬π' K`,
  -- which completes the proof.
  rcases h.ultrafilter_le_nhds_of_tendsto hy with ‚ü®x, rfl, hx‚ü©
  exact ‚ü®x, hyK, hx‚ü©

/-- A map is proper if and only if it is closed and its fibers are compact. -/
theorem isProperMap_iff_isClosedMap_and_compact_fibers :
    IsProperMap f ‚Üî Continuous f ‚àß IsClosedMap f ‚àß ‚àÄ y, IsCompact (f ‚Åª¬π' {y}) := by
  constructor <;> intro H
  -- Note: In Bourbaki, the direct implication is proved by going through universally closed maps.
  -- We could do the same (using a `TFAE` cycle) but proving it directly from
  -- `IsProperMap.isCompact_preimage` is nice enough already so we don't bother with that.
  ¬∑ exact ‚ü®H.continuous, H.isClosedMap, fun y ‚Ü¶ H.isCompact_preimage isCompact_singleton‚ü©
  ¬∑ rw [isProperMap_iff_clusterPt]
  -- Let `‚Ñ± : Filter X` and `y` some cluster point of `map f ‚Ñ±`.
    refine ‚ü®H.1, fun ‚Ñ± y hy ‚Ü¶ ?_‚ü©
  -- That means that the singleton `pure y` meets the "closure" of `map f ‚Ñ±`, by which we mean
  -- `Filter.lift' (map f ‚Ñ±) closure`. But `f` is closed, so
  -- `closure (map f ‚Ñ±) = map f (closure ‚Ñ±)` (see `IsClosedMap.lift'_closure_map_eq`).
  -- Thus `map f (closure ‚Ñ± ‚äì ùìü (f ‚Åª¬π' {y})) = map f (closure ‚Ñ±) ‚äì ùìü {y} ‚â† ‚ä•`, hence
  -- `closure ‚Ñ± ‚äì ùìü (f ‚Åª¬π' {y}) ‚â† ‚ä•`.
    rw [H.2.1.mapClusterPt_iff_lift'_closure H.1] at hy
  -- Now, applying the compactness of `f ‚Åª¬π' {y}` to the nontrivial filter
  -- `closure ‚Ñ± ‚äì ùìü (f ‚Åª¬π' {y})`, we obtain that it has a cluster point `x ‚àà f ‚Åª¬π' {y}`.
    rcases H.2.2 y (f := Filter.lift' ‚Ñ± closure ‚äì ùìü (f ‚Åª¬π' {y})) inf_le_right with ‚ü®x, hxy, hx‚ü©
    refine ‚ü®x, hxy, ?_‚ü©
  -- In particular `x` is a cluster point of `closure ‚Ñ±`. Since cluster points of `closure ‚Ñ±`
  -- are exactly cluster points of `‚Ñ±` (see `clusterPt_lift'_closure_iff`), this completes
  -- the proof.
    rw [‚Üê clusterPt_lift'_closure_iff]
    exact hx.mono inf_le_left

/-- An injective and continuous function is proper if and only if it is closed. -/
lemma isProperMap_iff_isClosedMap_of_inj (f_cont : Continuous f) (f_inj : f.Injective) :
    IsProperMap f ‚Üî IsClosedMap f := by
  refine ‚ü®fun h ‚Ü¶ h.isClosedMap, fun h ‚Ü¶ ?_‚ü©
  rw [isProperMap_iff_isClosedMap_and_compact_fibers]
  exact ‚ü®f_cont, h, fun y ‚Ü¶ (subsingleton_singleton.preimage f_inj).isCompact‚ü©

/-- A injective continuous and closed map is proper. -/
lemma isProperMap_of_isClosedMap_of_inj (f_cont : Continuous f) (f_inj : f.Injective)
    (f_closed : IsClosedMap f) : IsProperMap f :=
  (isProperMap_iff_isClosedMap_of_inj f_cont f_inj).2 f_closed

/-- A homeomorphism is proper. -/
@[simp] lemma Homeomorph.isProperMap (e : X ‚âÉ‚Çú Y) : IsProperMap e :=
  isProperMap_of_isClosedMap_of_inj e.continuous e.injective e.isClosedMap

protected lemma IsHomeomorph.isProperMap (hf : IsHomeomorph f) : IsProperMap f :=
  isProperMap_of_isClosedMap_of_inj hf.continuous hf.injective hf.isClosedMap

/-- The identity is proper. -/
@[simp] lemma isProperMap_id : IsProperMap (id : X ‚Üí X) := IsHomeomorph.id.isProperMap

/-- A closed embedding is proper. -/
lemma Topology.IsClosedEmbedding.isProperMap (hf : IsClosedEmbedding f) : IsProperMap f :=
  isProperMap_of_isClosedMap_of_inj hf.continuous hf.injective hf.isClosedMap

/-- The coercion from a closed subset is proper. -/
lemma IsClosed.isProperMap_subtypeVal {C : Set X} (hC : IsClosed C) : IsProperMap ((‚Üë) : C ‚Üí X) :=
  hC.isClosedEmbedding_subtypeVal.isProperMap

/-- The restriction of a proper map to a closed subset is proper. -/
lemma IsProperMap.restrict {C : Set X} (hf : IsProperMap f) (hC : IsClosed C) :
    IsProperMap fun x : C ‚Ü¶ f x := hf.comp hC.isProperMap_subtypeVal

/-- The range of a proper map is closed. -/
lemma IsProperMap.isClosed_range (hf : IsProperMap f) : IsClosed (range f) :=
  hf.isClosedMap.isClosed_range

/-- Version of `isProperMap_iff_isClosedMap_and_compact_fibers` in terms of `cofinite` and
`cocompact`. Only works when the codomain is `T1`. -/
lemma isProperMap_iff_isClosedMap_and_tendsto_cofinite [T1Space Y] :
    IsProperMap f ‚Üî Continuous f ‚àß IsClosedMap f ‚àß Tendsto f (cocompact X) cofinite := by
  simp_rw [isProperMap_iff_isClosedMap_and_compact_fibers, Tendsto,
    le_cofinite_iff_compl_singleton_mem, mem_map, preimage_compl]
  refine and_congr_right fun f_cont ‚Ü¶ and_congr_right fun _ ‚Ü¶
    ‚ü®fun H y ‚Ü¶ (H y).compl_mem_cocompact, fun H y ‚Ü¶ ?_‚ü©
  rcases mem_cocompact.mp (H y) with ‚ü®K, hK, hKy‚ü©
  exact hK.of_isClosed_subset (isClosed_singleton.preimage f_cont)
    (compl_le_compl_iff_le.mp hKy)

/-- A continuous map from a compact space to a T‚ÇÇ space is a proper map. -/
theorem Continuous.isProperMap [CompactSpace X] [T2Space Y] (hf : Continuous f) : IsProperMap f :=
  isProperMap_iff_isClosedMap_and_tendsto_cofinite.2 ‚ü®hf, hf.isClosedMap, by simp‚ü©

/-- A constant map to a T‚ÇÅ space is proper if and only if its domain is compact. -/
theorem isProperMap_const_iff [T1Space Y] (y : Y) :
    IsProperMap (fun _ : X ‚Ü¶ y) ‚Üî CompactSpace X := by
  classical
  rw [isProperMap_iff_isClosedMap_and_compact_fibers]
  constructor
  ¬∑ rintro ‚ü®-, -, h‚ü©
    exact ‚ü®by simpa using h y‚ü©
  ¬∑ intro H
    refine ‚ü®continuous_const, isClosedMap_const, fun y' ‚Ü¶ ?_‚ü©
    simp [preimage_const, mem_singleton_iff, apply_ite, isCompact_univ]

theorem isProperMap_const [h : CompactSpace X] [T1Space Y] (y : Y) :
    IsProperMap (fun _ : X ‚Ü¶ y) :=
  isProperMap_const_iff y |>.mpr h

/-- If `Y` is a compact topological space, then `Prod.fst : X √ó Y ‚Üí X` is a proper map. -/
theorem isProperMap_fst_of_compactSpace [CompactSpace Y] :
    IsProperMap (Prod.fst : X √ó Y ‚Üí X) :=
  Homeomorph.prodPUnit X |>.isProperMap.comp (isProperMap_id.prodMap (isProperMap_const ()))

/-- If `X` is a compact topological space, then `Prod.snd : X √ó Y ‚Üí Y` is a proper map. -/
theorem isProperMap_snd_of_compactSpace [CompactSpace X] :
    IsProperMap (Prod.snd : X √ó Y ‚Üí Y) :=
  Homeomorph.punitProd Y |>.isProperMap.comp ((isProperMap_const ()).prodMap isProperMap_id)

/-- If `Y` is a compact topological space, then `Prod.fst : X √ó Y ‚Üí X` is a closed map. -/
theorem isClosedMap_fst_of_compactSpace [CompactSpace Y] :
    IsClosedMap (Prod.fst : X √ó Y ‚Üí X) :=
  isProperMap_fst_of_compactSpace.isClosedMap

/-- If `X` is a compact topological space, then `Prod.snd : X √ó Y ‚Üí Y` is a closed map. -/
theorem isClosedMap_snd_of_compactSpace [CompactSpace X] :
    IsClosedMap (Prod.snd : X √ó Y ‚Üí Y) :=
  isProperMap_snd_of_compactSpace.isClosedMap

/-- A proper map `f : X ‚Üí Y` is **universally closed**: for any topological space `Z`, the map
`Prod.map f id : X √ó Z ‚Üí Y √ó Z` is closed. We will prove in `isProperMap_iff_universally_closed`
that proper maps are exactly continuous maps which have this property, but this result should be
easier to use because it allows `Z` to live in any universe. -/
theorem IsProperMap.universally_closed (Z) [TopologicalSpace Z] (h : IsProperMap f) :
    IsClosedMap (Prod.map f id : X √ó Z ‚Üí Y √ó Z) :=
  -- `f √ó id` is proper as a product of proper maps, hence closed.
  (h.prodMap isProperMap_id).isClosedMap
