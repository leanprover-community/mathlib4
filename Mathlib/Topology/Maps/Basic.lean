/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro, Patrick Massot
-/
import Mathlib.Topology.Order

/-!
# Specific classes of maps between topological spaces

This file introduces the following properties of a map `f : X ‚Üí Y` between topological spaces:

* `IsOpenMap f` means the image of an open set under `f` is open.
* `IsClosedMap f` means the image of a closed set under `f` is closed.

(Open and closed maps need not be continuous.)

* `IsInducing f` means the topology on `X` is the one induced via `f` from the topology on `Y`.
  These behave like embeddings except they need not be injective. Instead, points of `X` which
  are identified by `f` are also inseparable in the topology on `X`.
* `IsEmbedding f` means `f` is inducing and also injective. Equivalently, `f` identifies `X` with
  a subspace of `Y`.
* `IsOpenEmbedding f` means `f` is an embedding with open image, so it identifies `X` with an
  open subspace of `Y`. Equivalently, `f` is an embedding and an open map.
* `IsClosedEmbedding f` similarly means `f` is an embedding with closed image, so it identifies
  `X` with a closed subspace of `Y`. Equivalently, `f` is an embedding and a closed map.

* `IsQuotientMap f` is the dual condition to `IsEmbedding f`: `f` is surjective and the topology
  on `Y` is the one coinduced via `f` from the topology on `X`. Equivalently, `f` identifies
  `Y` with a quotient of `X`. Quotient maps are also sometimes known as identification maps.

## References

* <https://en.wikipedia.org/wiki/Open_and_closed_maps>
* <https://en.wikipedia.org/wiki/Embedding#General_topology>
* <https://en.wikipedia.org/wiki/Quotient_space_(topology)#Quotient_map>

## Tags

open map, closed map, embedding, quotient map, identification map

-/


open Set Filter Function

open TopologicalSpace Topology Filter

variable {X : Type*} {Y : Type*} {Z : Type*} {Œπ : Type*} {f : X ‚Üí Y} {g : Y ‚Üí Z}

namespace Topology
section IsInducing

variable [TopologicalSpace Y]

protected lemma IsInducing.induced (f : X ‚Üí Y) : @IsInducing X Y (induced f ‚Äπ_‚Ä∫) _ f :=
  @IsInducing.mk _ _ (TopologicalSpace.induced f ‚Äπ_‚Ä∫) _ _ rfl

@[deprecated (since := "2024-10-28")] alias inducing_induced := IsInducing.induced

variable [TopologicalSpace X]

protected lemma IsInducing.id : IsInducing (@id X) := ‚ü®induced_id.symm‚ü©

@[deprecated (since := "2024-10-28")] alias inducing_id := IsInducing.id

variable [TopologicalSpace Z]

protected lemma IsInducing.comp (hg : IsInducing g) (hf : IsInducing f) :
    IsInducing (g ‚àò f) :=
  ‚ü®by rw [hf.eq_induced, hg.eq_induced, induced_compose]‚ü©

@[deprecated (since := "2024-10-28")] alias Inducing.comp := IsInducing.comp

lemma IsInducing.of_comp_iff (hg : IsInducing g) : IsInducing (g ‚àò f) ‚Üî IsInducing f := by
  refine ‚ü®fun h ‚Ü¶ ?_, hg.comp‚ü©
  rw [isInducing_iff, hg.eq_induced, induced_compose, h.eq_induced]

@[deprecated (since := "2024-10-28")] alias Inducing.of_comp_iff := IsInducing.of_comp_iff

lemma IsInducing.of_comp (hf : Continuous f) (hg : Continuous g) (hgf : IsInducing (g ‚àò f)) :
    IsInducing f :=
  ‚ü®le_antisymm (by rwa [‚Üê continuous_iff_le_induced])
      (by
        rw [hgf.eq_induced, ‚Üê induced_compose]
        exact induced_mono hg.le_induced)‚ü©

@[deprecated (since := "2024-10-28")] alias inducing_of_inducing_compose := IsInducing.of_comp

lemma isInducing_iff_nhds : IsInducing f ‚Üî ‚àÄ x, ùìù x = comap f (ùìù (f x)) :=
  (isInducing_iff _).trans (induced_iff_nhds_eq f)

@[deprecated (since := "2024-10-28")] alias inducing_iff_nhds := isInducing_iff_nhds

namespace IsInducing

lemma nhds_eq_comap (hf : IsInducing f) : ‚àÄ x : X, ùìù x = comap f (ùìù <| f x) :=
  isInducing_iff_nhds.1 hf

lemma basis_nhds {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí Set Y} (hf : IsInducing f) {x : X}
    (h_basis : (ùìù (f x)).HasBasis p s) : (ùìù x).HasBasis p (preimage f ‚àò s) :=
  hf.nhds_eq_comap x ‚ñ∏ h_basis.comap f

lemma nhdsSet_eq_comap (hf : IsInducing f) (s : Set X) :
    ùìùÀ¢ s = comap f (ùìùÀ¢ (f '' s)) := by
  simp only [nhdsSet, sSup_image, comap_iSup, hf.nhds_eq_comap, iSup_image]

lemma map_nhds_eq (hf : IsInducing f) (x : X) : (ùìù x).map f = ùìù[range f] f x :=
  hf.eq_induced ‚ñ∏ map_nhds_induced_eq x

lemma map_nhds_of_mem (hf : IsInducing f) (x : X) (h : range f ‚àà ùìù (f x)) :
    (ùìù x).map f = ùìù (f x) := hf.eq_induced ‚ñ∏ map_nhds_induced_of_mem h

lemma mapClusterPt_iff (hf : IsInducing f) {x : X} {l : Filter X} :
    MapClusterPt (f x) l f ‚Üî ClusterPt x l := by
  delta MapClusterPt ClusterPt
  rw [‚Üê Filter.push_pull', ‚Üê hf.nhds_eq_comap, map_neBot_iff]

lemma image_mem_nhdsWithin (hf : IsInducing f) {x : X} {s : Set X} (hs : s ‚àà ùìù x) :
    f '' s ‚àà ùìù[range f] f x :=
  hf.map_nhds_eq x ‚ñ∏ image_mem_map hs

lemma tendsto_nhds_iff {f : Œπ ‚Üí Y} {l : Filter Œπ} {y : Y} (hg : IsInducing g) :
    Tendsto f l (ùìù y) ‚Üî Tendsto (g ‚àò f) l (ùìù (g y)) := by
  rw [hg.nhds_eq_comap, tendsto_comap_iff]

lemma continuousAt_iff (hg : IsInducing g) {x : X} :
    ContinuousAt f x ‚Üî ContinuousAt (g ‚àò f) x :=
  hg.tendsto_nhds_iff

lemma continuous_iff (hg : IsInducing g) :
    Continuous f ‚Üî Continuous (g ‚àò f) := by
  simp_rw [continuous_iff_continuousAt, hg.continuousAt_iff]

lemma continuousAt_iff' (hf : IsInducing f) {x : X} (h : range f ‚àà ùìù (f x)) :
    ContinuousAt (g ‚àò f) x ‚Üî ContinuousAt g (f x) := by
  simp_rw [ContinuousAt, Filter.Tendsto, ‚Üê hf.map_nhds_of_mem _ h, Filter.map_map, comp]

protected lemma continuous (hf : IsInducing f) : Continuous f :=
  hf.continuous_iff.mp continuous_id

lemma closure_eq_preimage_closure_image (hf : IsInducing f) (s : Set X) :
    closure s = f ‚Åª¬π' closure (f '' s) := by
  ext x
  rw [Set.mem_preimage, ‚Üê closure_induced, hf.eq_induced]

theorem isClosed_iff (hf : IsInducing f) {s : Set X} :
    IsClosed s ‚Üî ‚àÉ t, IsClosed t ‚àß f ‚Åª¬π' t = s := by rw [hf.eq_induced, isClosed_induced_iff]

theorem isClosed_iff' (hf : IsInducing f) {s : Set X} :
    IsClosed s ‚Üî ‚àÄ x, f x ‚àà closure (f '' s) ‚Üí x ‚àà s := by rw [hf.eq_induced, isClosed_induced_iff']

theorem isClosed_preimage (h : IsInducing f) (s : Set Y) (hs : IsClosed s) :
    IsClosed (f ‚Åª¬π' s) :=
  (isClosed_iff h).mpr ‚ü®s, hs, rfl‚ü©

theorem isOpen_iff (hf : IsInducing f) {s : Set X} :
    IsOpen s ‚Üî ‚àÉ t, IsOpen t ‚àß f ‚Åª¬π' t = s := by rw [hf.eq_induced, isOpen_induced_iff]

theorem setOf_isOpen (hf : IsInducing f) :
    {s : Set X | IsOpen s} = preimage f '' {t | IsOpen t} :=
  Set.ext fun _ ‚Ü¶ hf.isOpen_iff

theorem dense_iff (hf : IsInducing f) {s : Set X} :
    Dense s ‚Üî ‚àÄ x, f x ‚àà closure (f '' s) := by
  simp only [Dense, hf.closure_eq_preimage_closure_image, mem_preimage]

theorem of_subsingleton [Subsingleton X] (f : X ‚Üí Y) : IsInducing f :=
  ‚ü®Subsingleton.elim _ _‚ü©

end IsInducing.IsInducing

namespace IsEmbedding

lemma induced [t : TopologicalSpace Y] (hf : Injective f) :
    @IsEmbedding X Y (t.induced f) t f :=
  @IsEmbedding.mk X Y (t.induced f) t _ (.induced f) hf

alias _root_.Function.Injective.isEmbedding_induced := IsEmbedding.induced

@[deprecated (since := "2024-10-26")]
alias Function.Injective.embedding_induced := _root_.Function.Injective.isEmbedding_induced

variable [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]

lemma isInducing (hf : IsEmbedding f) : IsInducing f := hf.toIsInducing

@[deprecated (since := "2024-10-28")] alias inducing := isInducing

lemma mk' (f : X ‚Üí Y) (inj : Injective f) (induced : ‚àÄ x, comap f (ùìù (f x)) = ùìù x) :
    IsEmbedding f :=
  ‚ü®isInducing_iff_nhds.2 fun x => (induced x).symm, inj‚ü©

@[deprecated (since := "2024-10-26")]
alias Embedding.mk' := mk'

protected lemma id : IsEmbedding (@id X) := ‚ü®.id, fun _ _ h => h‚ü©

@[deprecated (since := "2024-10-26")]
alias embedding_id := IsEmbedding.id

protected lemma comp (hg : IsEmbedding g) (hf : IsEmbedding f) : IsEmbedding (g ‚àò f) :=
  { hg.isInducing.comp hf.isInducing with injective := fun _ _ h => hf.injective <| hg.injective h }

@[deprecated (since := "2024-10-26")]
alias Embedding.comp := IsEmbedding.comp

lemma of_comp_iff (hg : IsEmbedding g) : IsEmbedding (g ‚àò f) ‚Üî IsEmbedding f := by
  simp_rw [isEmbedding_iff, hg.isInducing.of_comp_iff, hg.injective.of_comp_iff f]

@[deprecated (since := "2024-10-26")]
alias Embedding.of_comp_iff := of_comp_iff

protected lemma of_comp (hf : Continuous f) (hg : Continuous g) (hgf : IsEmbedding (g ‚àò f)) :
    IsEmbedding f where
  toIsInducing := hgf.isInducing.of_comp hf hg
  injective := hgf.injective.of_comp

@[deprecated (since := "2024-10-26")]
alias embedding_of_embedding_compose := IsEmbedding.of_comp

lemma of_leftInverse {f : X ‚Üí Y} {g : Y ‚Üí X} (h : LeftInverse f g) (hf : Continuous f)
    (hg : Continuous g) : IsEmbedding g := .of_comp hg hf <| h.comp_eq_id.symm ‚ñ∏ .id

alias _root_.Function.LeftInverse.isEmbedding := of_leftInverse

@[deprecated (since := "2024-10-26")]
alias _root_.Function.LeftInverse.embedding := Function.LeftInverse.isEmbedding

lemma map_nhds_eq (hf : IsEmbedding f) (x : X) :     (ùìù x).map f = ùìù[range f] f x :=
  hf.1.map_nhds_eq x

@[deprecated (since := "2024-10-26")]
alias Embedding.map_nhds_eq := map_nhds_eq

lemma map_nhds_of_mem (hf : IsEmbedding f) (x : X) (h : range f ‚àà ùìù (f x)) :
    (ùìù x).map f = ùìù (f x) :=
  hf.1.map_nhds_of_mem x h

@[deprecated (since := "2024-10-26")]
alias Embedding.map_nhds_of_mem := map_nhds_of_mem

lemma tendsto_nhds_iff {f : Œπ ‚Üí Y} {l : Filter Œπ} {y : Y} (hg : IsEmbedding g) :
    Tendsto f l (ùìù y) ‚Üî Tendsto (g ‚àò f) l (ùìù (g y)) := hg.isInducing.tendsto_nhds_iff

lemma continuous_iff (hg : IsEmbedding g) : Continuous f ‚Üî Continuous (g ‚àò f) :=
  hg.isInducing.continuous_iff

@[deprecated (since := "2024-10-26")]
alias Embedding.continuous_iff := continuous_iff

lemma continuous (hf : IsEmbedding f) : Continuous f := hf.isInducing.continuous

lemma closure_eq_preimage_closure_image (hf : IsEmbedding f) (s : Set X) :
    closure s = f ‚Åª¬π' closure (f '' s) :=
  hf.1.closure_eq_preimage_closure_image s

@[deprecated (since := "2024-10-26")]
alias Embedding.closure_eq_preimage_closure_image := closure_eq_preimage_closure_image

/-- The topology induced under an inclusion `f : X ‚Üí Y` from a discrete topological space `Y`
is the discrete topology on `X`.

See also `DiscreteTopology.of_continuous_injective`. -/
lemma discreteTopology [DiscreteTopology Y] (hf : IsEmbedding f) : DiscreteTopology X :=
  .of_continuous_injective hf.continuous hf.injective

@[deprecated (since := "2024-10-26")]
alias Embedding.discreteTopology := discreteTopology

lemma of_subsingleton [Subsingleton X] (f : X ‚Üí Y) : IsEmbedding f :=
  ‚ü®.of_subsingleton f, f.injective_of_subsingleton‚ü©

@[deprecated (since := "2024-10-26")]
alias Embedding.of_subsingleton := of_subsingleton

end IsEmbedding

section IsQuotientMap

variable [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]

lemma isQuotientMap_iff : IsQuotientMap f ‚Üî Surjective f ‚àß ‚àÄ s, IsOpen s ‚Üî IsOpen (f ‚Åª¬π' s) :=
  (isQuotientMap_iff' _).trans <| and_congr Iff.rfl TopologicalSpace.ext_iff

@[deprecated (since := "2024-10-22")]
alias quotientMap_iff := isQuotientMap_iff

theorem isQuotientMap_iff_isClosed :
    IsQuotientMap f ‚Üî Surjective f ‚àß ‚àÄ s : Set Y, IsClosed s ‚Üî IsClosed (f ‚Åª¬π' s) :=
  isQuotientMap_iff.trans <| Iff.rfl.and <| compl_surjective.forall.trans <| by
    simp only [isOpen_compl_iff, preimage_compl]

@[deprecated (since := "2024-10-22")]
alias quotientMap_iff_closed := isQuotientMap_iff_isClosed
@[deprecated (since := "2024-11-19")]
alias isQuotientMap_iff_closed := isQuotientMap_iff_isClosed

namespace IsQuotientMap

protected theorem id : IsQuotientMap (@id X) :=
  ‚ü®fun x => ‚ü®x, rfl‚ü©, coinduced_id.symm‚ü©

protected theorem comp (hg : IsQuotientMap g) (hf : IsQuotientMap f) : IsQuotientMap (g ‚àò f) :=
  ‚ü®hg.surjective.comp hf.surjective, by rw [hg.eq_coinduced, hf.eq_coinduced, coinduced_compose]‚ü©

protected theorem of_comp (hf : Continuous f) (hg : Continuous g)
    (hgf : IsQuotientMap (g ‚àò f)) : IsQuotientMap g :=
  ‚ü®hgf.1.of_comp,
    le_antisymm
      (by rw [hgf.eq_coinduced, ‚Üê coinduced_compose]; exact coinduced_mono hf.coinduced_le)
      hg.coinduced_le‚ü©

@[deprecated (since := "2024-10-22")]
alias of_quotientMap_compose := IsQuotientMap.of_comp

theorem of_inverse {g : Y ‚Üí X} (hf : Continuous f) (hg : Continuous g) (h : LeftInverse g f) :
    IsQuotientMap g := .of_comp hf hg <| h.comp_eq_id.symm ‚ñ∏ IsQuotientMap.id

protected theorem continuous_iff (hf : IsQuotientMap f) : Continuous g ‚Üî Continuous (g ‚àò f) := by
  rw [continuous_iff_coinduced_le, continuous_iff_coinduced_le, hf.eq_coinduced, coinduced_compose]

protected theorem continuous (hf : IsQuotientMap f) : Continuous f :=
  hf.continuous_iff.mp continuous_id

protected lemma isOpen_preimage (hf : IsQuotientMap f) {s : Set Y} : IsOpen (f ‚Åª¬π' s) ‚Üî IsOpen s :=
  ((isQuotientMap_iff.1 hf).2 s).symm

protected theorem isClosed_preimage (hf : IsQuotientMap f) {s : Set Y} :
    IsClosed (f ‚Åª¬π' s) ‚Üî IsClosed s :=
  ((isQuotientMap_iff_isClosed.1 hf).2 s).symm

end IsQuotientMap

end Topology.IsQuotientMap

section OpenMap
variable [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]

namespace IsOpenMap

protected theorem id : IsOpenMap (@id X) := fun s hs => by rwa [image_id]

protected theorem comp (hg : IsOpenMap g) (hf : IsOpenMap f) :
    IsOpenMap (g ‚àò f) := fun s hs => by rw [image_comp]; exact hg _ (hf _ hs)

theorem isOpen_range (hf : IsOpenMap f) : IsOpen (range f) := by
  rw [‚Üê image_univ]
  exact hf _ isOpen_univ

theorem image_mem_nhds (hf : IsOpenMap f) {x : X} {s : Set X} (hx : s ‚àà ùìù x) : f '' s ‚àà ùìù (f x) :=
  let ‚ü®t, hts, ht, hxt‚ü© := mem_nhds_iff.1 hx
  mem_of_superset (IsOpen.mem_nhds (hf t ht) (mem_image_of_mem _ hxt)) (image_subset _ hts)

theorem range_mem_nhds (hf : IsOpenMap f) (x : X) : range f ‚àà ùìù (f x) :=
  hf.isOpen_range.mem_nhds <| mem_range_self _

theorem mapsTo_interior (hf : IsOpenMap f) {s : Set X} {t : Set Y} (h : MapsTo f s t) :
    MapsTo f (interior s) (interior t) :=
  mapsTo'.2 <|
    interior_maximal (h.mono interior_subset Subset.rfl).image_subset (hf _ isOpen_interior)

theorem image_interior_subset (hf : IsOpenMap f) (s : Set X) :
    f '' interior s ‚äÜ interior (f '' s) :=
  (hf.mapsTo_interior (mapsTo_image f s)).image_subset

theorem nhds_le (hf : IsOpenMap f) (x : X) : ùìù (f x) ‚â§ (ùìù x).map f :=
  le_map fun _ => hf.image_mem_nhds

theorem of_nhds_le (hf : ‚àÄ x, ùìù (f x) ‚â§ map f (ùìù x)) : IsOpenMap f := fun _s hs =>
  isOpen_iff_mem_nhds.2 fun _y ‚ü®_x, hxs, hxy‚ü© => hxy ‚ñ∏ hf _ (image_mem_map <| hs.mem_nhds hxs)

theorem of_sections
    (h : ‚àÄ x, ‚àÉ g : Y ‚Üí X, ContinuousAt g (f x) ‚àß g (f x) = x ‚àß RightInverse g f) : IsOpenMap f :=
  of_nhds_le fun x =>
    let ‚ü®g, hgc, hgx, hgf‚ü© := h x
    calc
      ùìù (f x) = map f (map g (ùìù (f x))) := by rw [map_map, hgf.comp_eq_id, map_id]
      _ ‚â§ map f (ùìù (g (f x))) := map_mono hgc
      _ = map f (ùìù x) := by rw [hgx]

theorem of_inverse {f' : Y ‚Üí X} (h : Continuous f') (l_inv : LeftInverse f f')
    (r_inv : RightInverse f f') : IsOpenMap f :=
  of_sections fun _ => ‚ü®f', h.continuousAt, r_inv _, l_inv‚ü©

/-- A continuous surjective open map is a quotient map. -/
theorem isQuotientMap (open_map : IsOpenMap f) (cont : Continuous f) (surj : Surjective f) :
    IsQuotientMap f :=
  isQuotientMap_iff.2
    ‚ü®surj, fun s => ‚ü®fun h => h.preimage cont, fun h => surj.image_preimage s ‚ñ∏ open_map _ h‚ü©‚ü©

@[deprecated (since := "2024-10-22")]
alias to_quotientMap := isQuotientMap

theorem interior_preimage_subset_preimage_interior (hf : IsOpenMap f) {s : Set Y} :
    interior (f ‚Åª¬π' s) ‚äÜ f ‚Åª¬π' interior s :=
  hf.mapsTo_interior (mapsTo_preimage _ _)

theorem preimage_interior_eq_interior_preimage (hf‚ÇÅ : IsOpenMap f) (hf‚ÇÇ : Continuous f)
    (s : Set Y) : f ‚Åª¬π' interior s = interior (f ‚Åª¬π' s) :=
  Subset.antisymm (preimage_interior_subset_interior_preimage hf‚ÇÇ)
    (interior_preimage_subset_preimage_interior hf‚ÇÅ)

theorem preimage_closure_subset_closure_preimage (hf : IsOpenMap f) {s : Set Y} :
    f ‚Åª¬π' closure s ‚äÜ closure (f ‚Åª¬π' s) := by
  rw [‚Üê compl_subset_compl]
  simp only [‚Üê interior_compl, ‚Üê preimage_compl, hf.interior_preimage_subset_preimage_interior]

theorem preimage_closure_eq_closure_preimage (hf : IsOpenMap f) (hfc : Continuous f) (s : Set Y) :
    f ‚Åª¬π' closure s = closure (f ‚Åª¬π' s) :=
  hf.preimage_closure_subset_closure_preimage.antisymm (hfc.closure_preimage_subset s)

theorem preimage_frontier_subset_frontier_preimage (hf : IsOpenMap f) {s : Set Y} :
    f ‚Åª¬π' frontier s ‚äÜ frontier (f ‚Åª¬π' s) := by
  simpa only [frontier_eq_closure_inter_closure, preimage_inter] using
    inter_subset_inter hf.preimage_closure_subset_closure_preimage
      hf.preimage_closure_subset_closure_preimage

theorem preimage_frontier_eq_frontier_preimage (hf : IsOpenMap f) (hfc : Continuous f) (s : Set Y) :
    f ‚Åª¬π' frontier s = frontier (f ‚Åª¬π' s) := by
  simp only [frontier_eq_closure_inter_closure, preimage_inter, preimage_compl,
    hf.preimage_closure_eq_closure_preimage hfc]

theorem of_isEmpty [h : IsEmpty X] (f : X ‚Üí Y) : IsOpenMap f := of_nhds_le h.elim

end IsOpenMap

theorem isOpenMap_iff_nhds_le : IsOpenMap f ‚Üî ‚àÄ x : X, ùìù (f x) ‚â§ (ùìù x).map f :=
  ‚ü®fun hf => hf.nhds_le, IsOpenMap.of_nhds_le‚ü©

theorem isOpenMap_iff_interior : IsOpenMap f ‚Üî ‚àÄ s, f '' interior s ‚äÜ interior (f '' s) :=
  ‚ü®IsOpenMap.image_interior_subset, fun hs u hu =>
    subset_interior_iff_isOpen.mp <|
      calc
        f '' u = f '' interior u := by rw [hu.interior_eq]
        _ ‚äÜ interior (f '' u) := hs u‚ü©

/-- An inducing map with an open range is an open map. -/
protected lemma Topology.IsInducing.isOpenMap (hi : IsInducing f) (ho : IsOpen (range f)) :
    IsOpenMap f :=
  IsOpenMap.of_nhds_le fun _ => (hi.map_nhds_of_mem _ <| IsOpen.mem_nhds ho <| mem_range_self _).ge

@[deprecated (since := "2024-10-28")] alias Inducing.isOpenMap := IsInducing.isOpenMap

/-- Preimage of a dense set under an open map is dense. -/
protected theorem Dense.preimage {s : Set Y} (hs : Dense s) (hf : IsOpenMap f) :
    Dense (f ‚Åª¬π' s) := fun x ‚Ü¶
  hf.preimage_closure_subset_closure_preimage <| hs (f x)

end OpenMap

section IsClosedMap

variable [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]

namespace IsClosedMap
open Function

protected theorem id : IsClosedMap (@id X) := fun s hs => by rwa [image_id]

protected theorem comp (hg : IsClosedMap g) (hf : IsClosedMap f) : IsClosedMap (g ‚àò f) := by
  intro s hs
  rw [image_comp]
  exact hg _ (hf _ hs)

protected theorem of_comp_surjective (hf : Surjective f) (hf' : Continuous f)
    (hfg : IsClosedMap (g ‚àò f)) : IsClosedMap g := by
  intro K hK
  rw [‚Üê image_preimage_eq K hf, ‚Üê image_comp]
  exact hfg _ (hK.preimage hf')

theorem closure_image_subset (hf : IsClosedMap f) (s : Set X) :
    closure (f '' s) ‚äÜ f '' closure s :=
  closure_minimal (image_subset _ subset_closure) (hf _ isClosed_closure)

theorem of_inverse {f' : Y ‚Üí X} (h : Continuous f') (l_inv : LeftInverse f f')
    (r_inv : RightInverse f f') : IsClosedMap f := fun s hs => by
  rw [image_eq_preimage_of_inverse r_inv l_inv]
  exact hs.preimage h

theorem of_nonempty (h : ‚àÄ s, IsClosed s ‚Üí s.Nonempty ‚Üí IsClosed (f '' s)) :
    IsClosedMap f := by
  intro s hs; rcases eq_empty_or_nonempty s with h2s | h2s
  ¬∑ simp_rw [h2s, image_empty, isClosed_empty]
  ¬∑ exact h s hs h2s

theorem isClosed_range (hf : IsClosedMap f) : IsClosed (range f) :=
  @image_univ _ _ f ‚ñ∏ hf _ isClosed_univ

@[deprecated (since := "2024-03-17")] alias closed_range := isClosed_range

theorem isQuotientMap (hcl : IsClosedMap f) (hcont : Continuous f)
    (hsurj : Surjective f) : IsQuotientMap f :=
  isQuotientMap_iff_isClosed.2 ‚ü®hsurj, fun s =>
    ‚ü®fun hs => hs.preimage hcont, fun hs => hsurj.image_preimage s ‚ñ∏ hcl _ hs‚ü©‚ü©

@[deprecated (since := "2024-10-22")]
alias to_quotientMap := isQuotientMap

end IsClosedMap

lemma Topology.IsInducing.isClosedMap (hf : IsInducing f) (h : IsClosed (range f)) :
    IsClosedMap f := by
  intro s hs
  rcases hf.isClosed_iff.1 hs with ‚ü®t, ht, rfl‚ü©
  rw [image_preimage_eq_inter_range]
  exact ht.inter h

@[deprecated (since := "2024-10-28")] alias Inducing.isClosedMap := IsInducing.isClosedMap

theorem isClosedMap_iff_closure_image :
    IsClosedMap f ‚Üî ‚àÄ s, closure (f '' s) ‚äÜ f '' closure s :=
  ‚ü®IsClosedMap.closure_image_subset, fun hs c hc =>
    isClosed_of_closure_subset <|
      calc
        closure (f '' c) ‚äÜ f '' closure c := hs c
        _ = f '' c := by rw [hc.closure_eq]‚ü©

/-- A map `f : X ‚Üí Y` is closed if and only if for all sets `s`, any cluster point of `f '' s` is
the image by `f` of some cluster point of `s`.
If you require this for all filters instead of just principal filters, and also that `f` is
continuous, you get the notion of **proper map**. See `isProperMap_iff_clusterPt`. -/
theorem isClosedMap_iff_clusterPt :
    IsClosedMap f ‚Üî ‚àÄ s y, MapClusterPt y (ùìü s) f ‚Üí ‚àÉ x, f x = y ‚àß ClusterPt x (ùìü s) := by
  simp [MapClusterPt, isClosedMap_iff_closure_image, subset_def, mem_closure_iff_clusterPt,
    and_comm]

theorem IsClosedMap.closure_image_eq_of_continuous
    (f_closed : IsClosedMap f) (f_cont : Continuous f) (s : Set X) :
    closure (f '' s) = f '' closure s :=
  subset_antisymm (f_closed.closure_image_subset s) (image_closure_subset_closure_image f_cont)

theorem IsClosedMap.lift'_closure_map_eq
    (f_closed : IsClosedMap f) (f_cont : Continuous f) (F : Filter X) :
    (map f F).lift' closure = map f (F.lift' closure) := by
  rw [map_lift'_eq2 (monotone_closure Y), map_lift'_eq (monotone_closure X)]
  congr
  ext s : 1
  exact f_closed.closure_image_eq_of_continuous f_cont s

theorem IsClosedMap.mapClusterPt_iff_lift'_closure
    {F : Filter X} (f_closed : IsClosedMap f) (f_cont : Continuous f) {y : Y} :
    MapClusterPt y F f ‚Üî ((F.lift' closure) ‚äì ùìü (f ‚Åª¬π' {y})).NeBot := by
  rw [MapClusterPt, clusterPt_iff_lift'_closure', f_closed.lift'_closure_map_eq f_cont,
      ‚Üê comap_principal, ‚Üê map_neBot_iff f, Filter.push_pull, principal_singleton]

end IsClosedMap

namespace Topology
section IsOpenEmbedding

variable [TopologicalSpace X] [TopologicalSpace Y]

lemma IsOpenEmbedding.isEmbedding (hf : IsOpenEmbedding f) : IsEmbedding f := hf.toIsEmbedding
lemma IsOpenEmbedding.isInducing (hf : IsOpenEmbedding f) : IsInducing f :=
  hf.isEmbedding.isInducing

@[deprecated (since := "2024-10-28")] alias IsOpenEmbedding.inducing := IsOpenEmbedding.isInducing

lemma IsOpenEmbedding.isOpenMap (hf : IsOpenEmbedding f) : IsOpenMap f :=
  hf.isEmbedding.isInducing.isOpenMap hf.isOpen_range

@[deprecated (since := "2024-10-18")]
alias OpenEmbedding.isOpenMap := IsOpenEmbedding.isOpenMap

theorem IsOpenEmbedding.map_nhds_eq (hf : IsOpenEmbedding f) (x : X) :
    map f (ùìù x) = ùìù (f x) :=
  hf.isEmbedding.map_nhds_of_mem _ <| hf.isOpen_range.mem_nhds <| mem_range_self _

@[deprecated (since := "2024-10-18")]
alias OpenEmbedding.map_nhds_eq := IsOpenEmbedding.map_nhds_eq

lemma IsOpenEmbedding.isOpen_iff_image_isOpen (hf : IsOpenEmbedding f) {s : Set X} :
    IsOpen s ‚Üî IsOpen (f '' s) where
  mp := hf.isOpenMap s
  mpr h := by convert ‚Üê h.preimage hf.isEmbedding.continuous; apply preimage_image_eq _ hf.injective

@[deprecated (since := "2024-10-30")]
alias IsOpenEmbedding.open_iff_image_open := IsOpenEmbedding.isOpen_iff_image_isOpen

@[deprecated (since := "2024-10-18")]
alias OpenEmbedding.open_iff_image_open := IsOpenEmbedding.isOpen_iff_image_isOpen

theorem IsOpenEmbedding.tendsto_nhds_iff [TopologicalSpace Z] {f : Œπ ‚Üí Y} {l : Filter Œπ} {y : Y}
    (hg : IsOpenEmbedding g) : Tendsto f l (ùìù y) ‚Üî Tendsto (g ‚àò f) l (ùìù (g y)) :=
  hg.isEmbedding.tendsto_nhds_iff

@[deprecated (since := "2024-10-18")]
alias OpenEmbedding.tendsto_nhds_iff := IsOpenEmbedding.tendsto_nhds_iff

theorem IsOpenEmbedding.tendsto_nhds_iff' (hf : IsOpenEmbedding f) {l : Filter Z} {x : X} :
    Tendsto (g ‚àò f) (ùìù x) l ‚Üî Tendsto g (ùìù (f x)) l := by
  rw [Tendsto, ‚Üê map_map, hf.map_nhds_eq]; rfl

@[deprecated (since := "2024-10-18")]
alias OpenEmbedding.tendsto_nhds_iff' := IsOpenEmbedding.tendsto_nhds_iff'

theorem IsOpenEmbedding.continuousAt_iff [TopologicalSpace Z] (hf : IsOpenEmbedding f) {x : X} :
    ContinuousAt (g ‚àò f) x ‚Üî ContinuousAt g (f x) :=
  hf.tendsto_nhds_iff'

@[deprecated (since := "2024-10-18")]
alias OpenEmbedding.continuousAt_iff := IsOpenEmbedding.continuousAt_iff

theorem IsOpenEmbedding.continuous (hf : IsOpenEmbedding f) : Continuous f :=
  hf.isEmbedding.continuous

@[deprecated (since := "2024-10-18")]
alias OpenEmbedding.continuous := IsOpenEmbedding.continuous

lemma IsOpenEmbedding.isOpen_iff_preimage_isOpen (hf : IsOpenEmbedding f) {s : Set Y}
    (hs : s ‚äÜ range f) : IsOpen s ‚Üî IsOpen (f ‚Åª¬π' s) := by
  rw [hf.isOpen_iff_image_isOpen, image_preimage_eq_inter_range, inter_eq_self_of_subset_left hs]

@[deprecated (since := "2024-10-30")]
alias IsOpenEmbedding.open_iff_preimage_open := IsOpenEmbedding.isOpen_iff_preimage_isOpen

@[deprecated (since := "2024-10-18")]
alias OpenEmbedding.open_iff_preimage_open := IsOpenEmbedding.isOpen_iff_preimage_isOpen

lemma IsOpenEmbedding.of_isEmbedding_isOpenMap (h‚ÇÅ : IsEmbedding f) (h‚ÇÇ : IsOpenMap f) :
    IsOpenEmbedding f :=
  ‚ü®h‚ÇÅ, h‚ÇÇ.isOpen_range‚ü©

@[deprecated (since := "2024-10-26")]
alias isOpenEmbedding_of_embedding_open := IsOpenEmbedding.of_isEmbedding_isOpenMap

@[deprecated (since := "2024-10-18")]
alias openEmbedding_of_embedding_open := IsOpenEmbedding.of_isEmbedding_isOpenMap

/-- A surjective embedding is an `IsOpenEmbedding`. -/
lemma IsEmbedding.isOpenEmbedding_of_surjective (hf : IsEmbedding f) (hsurj : f.Surjective) :
    IsOpenEmbedding f :=
  ‚ü®hf, hsurj.range_eq ‚ñ∏ isOpen_univ‚ü©

@[deprecated (since := "2024-10-26")]
alias _root_.Embedding.toIsOpenEmbedding_of_surjective := IsEmbedding.isOpenEmbedding_of_surjective

alias IsOpenEmbedding.of_isEmbedding := IsEmbedding.isOpenEmbedding_of_surjective

@[deprecated (since := "2024-10-18")]
alias _root_.Embedding.toOpenEmbedding_of_surjective := IsEmbedding.isOpenEmbedding_of_surjective

lemma isOpenEmbedding_iff_isEmbedding_isOpenMap : IsOpenEmbedding f ‚Üî IsEmbedding f ‚àß IsOpenMap f :=
  ‚ü®fun h => ‚ü®h.1, h.isOpenMap‚ü©, fun h => .of_isEmbedding_isOpenMap h.1 h.2‚ü©

@[deprecated (since := "2024-10-26")]
alias isOpenEmbedding_iff_embedding_open := isOpenEmbedding_iff_isEmbedding_isOpenMap

@[deprecated (since := "2024-10-18")]
alias openEmbedding_iff_embedding_open := isOpenEmbedding_iff_isEmbedding_isOpenMap

theorem IsOpenEmbedding.of_continuous_injective_isOpenMap
    (h‚ÇÅ : Continuous f) (h‚ÇÇ : Injective f) (h‚ÇÉ : IsOpenMap f) : IsOpenEmbedding f := by
  simp only [isOpenEmbedding_iff_isEmbedding_isOpenMap, isEmbedding_iff, isInducing_iff_nhds, *,
    and_true]
  exact fun x =>
    le_antisymm (h‚ÇÅ.tendsto _).le_comap (@comap_map _ _ (ùìù x) _ h‚ÇÇ ‚ñ∏ comap_mono (h‚ÇÉ.nhds_le _))

lemma isOpenEmbedding_iff_continuous_injective_isOpenMap :
    IsOpenEmbedding f ‚Üî Continuous f ‚àß Injective f ‚àß IsOpenMap f :=
  ‚ü®fun h => ‚ü®h.continuous, h.injective, h.isOpenMap‚ü©, fun h =>
    .of_continuous_injective_isOpenMap h.1 h.2.1 h.2.2‚ü©

@[deprecated (since := "2024-10-30")]
alias isOpenEmbedding_iff_continuous_injective_open :=
  isOpenEmbedding_iff_continuous_injective_isOpenMap

@[deprecated (since := "2024-10-18")]
alias openEmbedding_iff_continuous_injective_open :=
  isOpenEmbedding_iff_continuous_injective_isOpenMap

namespace IsOpenEmbedding
variable [TopologicalSpace Z]

protected lemma id : IsOpenEmbedding (@id X) := ‚ü®.id, IsOpenMap.id.isOpen_range‚ü©

@[deprecated (since := "2024-10-18")]
alias _root_.openEmbedding_id := IsOpenEmbedding.id

protected lemma comp (hg : IsOpenEmbedding g)
    (hf : IsOpenEmbedding f) : IsOpenEmbedding (g ‚àò f) :=
  ‚ü®hg.1.comp hf.1, (hg.isOpenMap.comp hf.isOpenMap).isOpen_range‚ü©

theorem isOpenMap_iff (hg : IsOpenEmbedding g) :
    IsOpenMap f ‚Üî IsOpenMap (g ‚àò f) := by
  simp_rw [isOpenMap_iff_nhds_le, ‚Üê map_map, comp, ‚Üê hg.map_nhds_eq, map_le_map_iff hg.injective]

theorem of_comp_iff (f : X ‚Üí Y) (hg : IsOpenEmbedding g) :
    IsOpenEmbedding (g ‚àò f) ‚Üî IsOpenEmbedding f := by
  simp only [isOpenEmbedding_iff_continuous_injective_isOpenMap, ‚Üê hg.isOpenMap_iff, ‚Üê
    hg.1.continuous_iff, hg.injective.of_comp_iff]

lemma of_comp (f : X ‚Üí Y) (hg : IsOpenEmbedding g) (h : IsOpenEmbedding (g ‚àò f)) :
    IsOpenEmbedding f := (IsOpenEmbedding.of_comp_iff f hg).1 h

theorem of_isEmpty [IsEmpty X] (f : X ‚Üí Y) : IsOpenEmbedding f :=
  of_isEmbedding_isOpenMap (.of_subsingleton f) (.of_isEmpty f)

theorem image_mem_nhds {f : X ‚Üí Y} (hf : IsOpenEmbedding f) {s : Set X} {x : X} :
    f '' s ‚àà ùìù (f x) ‚Üî s ‚àà ùìù x := by
  rw [‚Üê hf.map_nhds_eq, mem_map, preimage_image_eq _ hf.injective]

end IsOpenEmbedding

end IsOpenEmbedding

section IsClosedEmbedding

variable [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]

namespace IsClosedEmbedding

lemma isEmbedding (hf : IsClosedEmbedding f) : IsEmbedding f := hf.toIsEmbedding
lemma isInducing (hf : IsClosedEmbedding f) : IsInducing f := hf.isEmbedding.isInducing
lemma continuous (hf : IsClosedEmbedding f) : Continuous f := hf.isEmbedding.continuous

@[deprecated (since := "2024-10-28")] alias inducing := isInducing

lemma tendsto_nhds_iff {g : Œπ ‚Üí X} {l : Filter Œπ} {x : X} (hf : IsClosedEmbedding f) :
    Tendsto g l (ùìù x) ‚Üî Tendsto (f ‚àò g) l (ùìù (f x)) := hf.isEmbedding.tendsto_nhds_iff

lemma isClosedMap (hf : IsClosedEmbedding f) : IsClosedMap f :=
  hf.isEmbedding.isInducing.isClosedMap hf.isClosed_range

lemma isClosed_iff_image_isClosed (hf : IsClosedEmbedding f) {s : Set X} :
    IsClosed s ‚Üî IsClosed (f '' s) :=
  ‚ü®hf.isClosedMap s, fun h => by
    rw [‚Üê preimage_image_eq s hf.injective]
    exact h.preimage hf.continuous‚ü©

@[deprecated (since := "2024-10-30")] alias closed_iff_image_closed := isClosed_iff_image_isClosed

lemma isClosed_iff_preimage_isClosed (hf : IsClosedEmbedding f) {s : Set Y}
    (hs : s ‚äÜ range f) : IsClosed s ‚Üî IsClosed (f ‚Åª¬π' s) := by
  rw [hf.isClosed_iff_image_isClosed, image_preimage_eq_of_subset hs]

@[deprecated (since := "2024-10-30")]
alias closed_iff_preimage_closed := isClosed_iff_preimage_isClosed

lemma of_isEmbedding_isClosedMap (h‚ÇÅ : IsEmbedding f) (h‚ÇÇ : IsClosedMap f) :
    IsClosedEmbedding f :=
  ‚ü®h‚ÇÅ, image_univ (f := f) ‚ñ∏ h‚ÇÇ univ isClosed_univ‚ü©

@[deprecated (since := "2024-10-26")]
alias _root_.IsClosedEmbedding.of_embedding_closed := of_isEmbedding_isClosedMap

@[deprecated (since := "2024-10-20")]
alias _root_.closedEmbedding_of_embedding_closed := of_isEmbedding_isClosedMap

lemma of_continuous_injective_isClosedMap (h‚ÇÅ : Continuous f) (h‚ÇÇ : Injective f)
    (h‚ÇÉ : IsClosedMap f) : IsClosedEmbedding f := by
  refine .of_isEmbedding_isClosedMap ‚ü®‚ü®?_‚ü©, h‚ÇÇ‚ü© h‚ÇÉ
  refine h‚ÇÅ.le_induced.antisymm fun s hs => ?_
  refine ‚ü®(f '' s·∂ú)·∂ú, (h‚ÇÉ _ hs.isClosed_compl).isOpen_compl, ?_‚ü©
  rw [preimage_compl, preimage_image_eq _ h‚ÇÇ, compl_compl]

@[deprecated (since := "2024-10-20")]
alias _root_.closedEmbedding_of_continuous_injective_closed :=
  IsClosedEmbedding.of_continuous_injective_isClosedMap

protected theorem id : IsClosedEmbedding (@id X) := ‚ü®.id, IsClosedMap.id.isClosed_range‚ü©

@[deprecated (since := "2024-10-20")]
alias _root_.closedEmbedding_id := IsClosedEmbedding.id

theorem comp (hg : IsClosedEmbedding g) (hf : IsClosedEmbedding f) :
    IsClosedEmbedding (g ‚àò f) :=
  ‚ü®hg.isEmbedding.comp hf.isEmbedding, (hg.isClosedMap.comp hf.isClosedMap).isClosed_range‚ü©

lemma of_comp_iff (hg : IsClosedEmbedding g) : IsClosedEmbedding (g ‚àò f) ‚Üî IsClosedEmbedding f := by
  simp_rw [isClosedEmbedding_iff, hg.isEmbedding.of_comp_iff, Set.range_comp,
    ‚Üê hg.isClosed_iff_image_isClosed]

@[deprecated (since := "2024-10-26")]
alias Embedding.of_comp_iff := of_comp_iff

theorem closure_image_eq (hf : IsClosedEmbedding f) (s : Set X) :
    closure (f '' s) = f '' closure s :=
  hf.isClosedMap.closure_image_eq_of_continuous hf.continuous s

end Topology.IsClosedEmbedding.IsClosedEmbedding
