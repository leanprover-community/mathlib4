/-
Copyright (c) 2024 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Topology.Maps.Basic

/-!
# Open quotient maps

An open quotient map is an open map `f : X ‚Üí Y` which is both an open map and a quotient map.
Equivalently, it is a surjective continuous open map.
We use the latter characterization as a definition.

Many important quotient maps are open quotient maps, including

- the quotient map from a topological space to its quotient by the action of a group;
- the quotient map from a topological group to its quotient by a normal subgroup;
- the quotient map from a topological spaace to its separation quotient.

Contrary to general quotient maps,
the category of open quotient maps is closed under `Prod.map`.
-/

open Filter Function Set Topology

variable {X Y Z : Type*} [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z] {f : X ‚Üí Y}

namespace IsOpenQuotientMap

protected theorem id : IsOpenQuotientMap (id : X ‚Üí X) := ‚ü®surjective_id, continuous_id, .id‚ü©

/-- An open quotient map is a quotient map. -/
theorem isQuotientMap (h : IsOpenQuotientMap f) : IsQuotientMap f :=
  h.isOpenMap.isQuotientMap h.continuous h.surjective

@[deprecated (since := "2024-10-22")]
alias quotientMap := isQuotientMap

theorem iff_isOpenMap_isQuotientMap : IsOpenQuotientMap f ‚Üî IsOpenMap f ‚àß IsQuotientMap f :=
  ‚ü®fun h ‚Ü¶ ‚ü®h.isOpenMap, h.isQuotientMap‚ü©, fun ‚ü®ho, hq‚ü© ‚Ü¶ ‚ü®hq.surjective, hq.continuous, ho‚ü©‚ü©

@[deprecated (since := "2024-10-22")]
alias iff_isOpenMap_quotientMap := iff_isOpenMap_isQuotientMap

theorem of_isOpenMap_isQuotientMap (ho : IsOpenMap f) (hq : IsQuotientMap f) :
    IsOpenQuotientMap f :=
  iff_isOpenMap_isQuotientMap.2 ‚ü®ho, hq‚ü©

@[deprecated (since := "2024-10-22")]
alias of_isOpenMap_quotientMap := of_isOpenMap_isQuotientMap

theorem comp {g : Y ‚Üí Z} (hg : IsOpenQuotientMap g) (hf : IsOpenQuotientMap f) :
    IsOpenQuotientMap (g ‚àò f) :=
  ‚ü®.comp hg.1 hf.1, .comp hg.2 hf.2, .comp hg.3 hf.3‚ü©

theorem map_nhds_eq (h : IsOpenQuotientMap f) (x : X) : map f (ùìù x) = ùìù (f x) :=
  le_antisymm h.continuous.continuousAt <| h.isOpenMap.nhds_le _

theorem continuous_comp_iff (h : IsOpenQuotientMap f) {g : Y ‚Üí Z} :
    Continuous (g ‚àò f) ‚Üî Continuous g :=
  h.isQuotientMap.continuous_iff.symm

theorem continuousAt_comp_iff (h : IsOpenQuotientMap f) {g : Y ‚Üí Z} {x : X} :
    ContinuousAt (g ‚àò f) x ‚Üî ContinuousAt g (f x) := by
  simp only [ContinuousAt, ‚Üê h.map_nhds_eq, tendsto_map'_iff, comp_def]

theorem dense_preimage_iff (h : IsOpenQuotientMap f) {s : Set Y} : Dense (f ‚Åª¬π' s) ‚Üî Dense s :=
  ‚ü®fun hs ‚Ü¶ h.surjective.denseRange.dense_of_mapsTo h.continuous hs (mapsTo_preimage _ _),
    fun hs ‚Ü¶ hs.preimage h.isOpenMap‚ü©

end IsOpenQuotientMap

theorem Topology.IsInducing.isOpenQuotientMap_of_surjective (ind : IsInducing f)
    (surj : Function.Surjective f) : IsOpenQuotientMap f where
  surjective := surj
  continuous := ind.continuous
  isOpenMap U U_open := by
    obtain ‚ü®V, hV, rfl‚ü© := ind.isOpen_iff.mp U_open
    rwa [V.image_preimage_eq surj]

theorem Topology.IsInducing.isQuotientMap_of_surjective (ind : IsInducing f)
    (surj : Function.Surjective f) : IsQuotientMap f :=
  (ind.isOpenQuotientMap_of_surjective surj).isQuotientMap

section Subquotient

variable {A B C D : Type*}
variable [TopologicalSpace A] [TopologicalSpace B] [TopologicalSpace C] [TopologicalSpace D]
variable (f : A ‚Üí B) (g : C ‚Üí D) (p : A ‚Üí C) (q : B ‚Üí D)

omit [TopologicalSpace C] in
/--
Given the following diagram with `f` inducing, `p` surjective,
`q` an open quotient map, and `g` injective. Suppose the image of `A` in `B` is stable
under the equivalence mod `q`, then the coinduced topology on `C` (from `A`)
coincides with the induced topology (from `D`).
```
A -f‚Üí B
‚à£     ‚à£
p     q
‚Üì     ‚Üì
C -g‚Üí D
```

A typical application is when `K ‚â§ H` are subgroups of `G`, then the quotient topology on `H/K`
is also the subspace topology from `G/K`.
-/
lemma coinduced_eq_induced_of_isOpenQuotientMap_of_isInducing
    (h : g ‚àò p = q ‚àò f)
    (hf : IsInducing f) (hp : Function.Surjective p)
    (hq : IsOpenQuotientMap q) (hg : Function.Injective g)
    (H : q ‚Åª¬π' (q '' (Set.range f)) ‚äÜ Set.range f) :
    ‚ÄπTopologicalSpace A‚Ä∫.coinduced p = ‚ÄπTopologicalSpace D‚Ä∫.induced g := by
  ext U
  show IsOpen (p ‚Åª¬π' U) ‚Üî ‚àÉ V, _
  simp_rw [hf.isOpen_iff,
    (Set.image_surjective.mpr hq.surjective).exists,
    ‚Üê hq.isQuotientMap.isOpen_preimage]
  constructor
  ¬∑ rintro ‚ü®V, hV, e‚ü©
    refine ‚ü®V, hq.continuous.1 _ (hq.isOpenMap _ hV), ?_‚ü©
    ext x
    obtain ‚ü®x, rfl‚ü© := hp x
    constructor
    ¬∑ rintro ‚ü®y, hy, e'‚ü©
      obtain ‚ü®y, rfl‚ü© := H ‚ü®_, ‚ü®x, rfl‚ü©, (e'.trans (congr_fun h x)).symm‚ü©
      rw [‚Üê hg ((congr_fun h y).trans e')]
      exact e.le hy
    ¬∑ intro H
      exact ‚ü®f x, e.ge H, congr_fun h.symm x‚ü©
  ¬∑ rintro ‚ü®V, hV, rfl‚ü©
    refine ‚ü®_, hV, ?_‚ü©
    simp_rw [‚Üê Set.preimage_comp, h]

lemma isEmbedding_of_isOpenQuotientMap_of_isInducing
    (h : g ‚àò p = q ‚àò f)
    (hf : IsInducing f) (hp : IsQuotientMap p)
    (hq : IsOpenQuotientMap q) (hg : Function.Injective g)
    (H : q ‚Åª¬π' (q '' (Set.range f)) ‚äÜ Set.range f) :
    IsEmbedding g :=
  ‚ü®‚ü®hp.eq_coinduced.trans (coinduced_eq_induced_of_isOpenQuotientMap_of_isInducing
    f g p q h hf hp.surjective hq hg H)‚ü©, hg‚ü©

lemma isQuotientMap_of_isOpenQuotientMap_of_isInducing
    (h : g ‚àò p = q ‚àò f)
    (hf : IsInducing f) (hp : Surjective p)
    (hq : IsOpenQuotientMap q) (hg : IsEmbedding g)
    (H : q ‚Åª¬π' (q '' (Set.range f)) ‚äÜ Set.range f) :
    IsQuotientMap p :=
  ‚ü®hp, hg.eq_induced.trans ((coinduced_eq_induced_of_isOpenQuotientMap_of_isInducing
    f g p q h hf hp hq hg.injective H)).symm‚ü©

end Subquotient
