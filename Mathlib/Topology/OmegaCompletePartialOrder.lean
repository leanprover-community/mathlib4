/-
Copyright (c) 2020 Simon Hudon. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Simon Hudon
-/
import Mathlib.Topology.Basic
import Mathlib.Order.UpperLower.Basic
import Mathlib.Order.OmegaCompletePartialOrder

#align_import topology.omega_complete_partial_order from "leanprover-community/mathlib"@"2705404e701abc6b3127da906f40bae062a169c9"

/-!
# Scott Topological Spaces

A type of topological spaces whose notion
of continuity is equivalent to continuity in Ï‰CPOs.

## Reference

 * https://ncatlab.org/nlab/show/Scott+topology

-/


open Set OmegaCompletePartialOrder

open Classical

universe u

-- "Scott", "Ï‰Sup"
set_option linter.uppercaseLean3 false

namespace Scott

/-- `x` is an `Ï‰`-Sup of a chain `c` if it is the least upper bound of the range of `c`. -/
def IsÏ‰Sup {Î± : Type u} [Preorder Î±] (c : Chain Î±) (x : Î±) : Prop :=
  (âˆ€ i, c i â‰¤ x) âˆ§ âˆ€ y, (âˆ€ i, c i â‰¤ y) â†’ x â‰¤ y
#align Scott.is_Ï‰Sup Scott.IsÏ‰Sup

theorem isÏ‰Sup_iff_isLUB {Î± : Type u} [Preorder Î±] {c : Chain Î±} {x : Î±} :
    IsÏ‰Sup c x â†” IsLUB (range c) x := by
  simp [IsÏ‰Sup, IsLUB, IsLeast, upperBounds, lowerBounds]
  -- ğŸ‰ no goals
#align Scott.is_Ï‰Sup_iff_is_lub Scott.isÏ‰Sup_iff_isLUB

variable (Î± : Type u) [OmegaCompletePartialOrder Î±]

/-- The characteristic function of open sets is monotone and preserves
the limits of chains. -/
def IsOpen (s : Set Î±) : Prop :=
  Continuous' fun x â†¦ x âˆˆ s
#align Scott.is_open Scott.IsOpen

theorem isOpen_univ : IsOpen Î± univ :=
  âŸ¨fun _ _ _ _ â†¦ mem_univ _, @CompleteLattice.top_continuous Î± Prop _ _âŸ©
#align Scott.is_open_univ Scott.isOpen_univ

theorem IsOpen.inter (s t : Set Î±) : IsOpen Î± s â†’ IsOpen Î± t â†’ IsOpen Î± (s âˆ© t) :=
  CompleteLattice.inf_continuous'
#align Scott.is_open.inter Scott.IsOpen.inter

theorem isOpen_sUnion (s : Set (Set Î±)) (hs : âˆ€ t âˆˆ s, IsOpen Î± t) : IsOpen Î± (â‹ƒâ‚€ s) := by
  simp only [IsOpen] at hs âŠ¢
  -- âŠ¢ Continuous' fun x => x âˆˆ â‹ƒâ‚€ s
  convert CompleteLattice.sSup_continuous' (setOf â»Â¹' s) hs
  -- âŠ¢ xâœ âˆˆ â‹ƒâ‚€ s â†” sSup (setOf â»Â¹' s) xâœ
  simp only [sSup_apply, setOf_bijective.surjective.exists, exists_prop, mem_preimage,
    SetCoe.exists, iSup_Prop_eq, mem_setOf_eq, mem_sUnion]
#align Scott.is_open_sUnion Scott.isOpen_sUnion

theorem IsOpen.isUpperSet {s : Set Î±} (hs : IsOpen Î± s) : IsUpperSet s := hs.fst

end Scott

/-- A Scott topological space is defined on preorders
such that their open sets, seen as a function `Î± â†’ Prop`,
preserves the joins of Ï‰-chains  -/
@[reducible]
def Scott (Î± : Type u) := Î±
#align Scott Scott

instance Scott.topologicalSpace (Î± : Type u) [OmegaCompletePartialOrder Î±] :
    TopologicalSpace (Scott Î±) where
  IsOpen := Scott.IsOpen Î±
  isOpen_univ := Scott.isOpen_univ Î±
  isOpen_inter := Scott.IsOpen.inter Î±
  isOpen_sUnion := Scott.isOpen_sUnion Î±
#align Scott.topological_space Scott.topologicalSpace

section notBelow

variable {Î± : Type*} [OmegaCompletePartialOrder Î±] (y : Scott Î±)

/-- `notBelow` is an open set in `Scott Î±` used
to prove the monotonicity of continuous functions -/
def notBelow :=
  { x | Â¬x â‰¤ y }
#align not_below notBelow

theorem notBelow_isOpen : IsOpen (notBelow y) := by
  have h : Monotone (notBelow y) := fun x z hle â†¦ mt hle.trans
  -- âŠ¢ IsOpen (notBelow y)
  refine âŸ¨h, fun c â†¦ eq_of_forall_ge_iff fun z â†¦ ?_âŸ©
  -- âŠ¢ â†‘{ toFun := fun x => x âˆˆ notBelow y, monotone' := h } (Ï‰Sup c) â‰¤ z â†” Ï‰Sup (C â€¦
  simp only [Ï‰Sup_le_iff, notBelow, mem_setOf_eq, le_Prop_eq, OrderHom.coe_mk, Chain.map_coe,
    Function.comp_apply, exists_imp, not_forall]
#align not_below_is_open notBelow_isOpen

end notBelow

open Scott hiding IsOpen

open OmegaCompletePartialOrder

theorem isÏ‰Sup_Ï‰Sup {Î±} [OmegaCompletePartialOrder Î±] (c : Chain Î±) : IsÏ‰Sup c (Ï‰Sup c) := by
  constructor
  -- âŠ¢ âˆ€ (i : â„•), â†‘c i â‰¤ Ï‰Sup c
  Â· apply le_Ï‰Sup
    -- ğŸ‰ no goals
  Â· apply Ï‰Sup_le
    -- ğŸ‰ no goals
#align is_Ï‰Sup_Ï‰Sup isÏ‰Sup_Ï‰Sup

theorem scottContinuous_of_continuous {Î± Î²} [OmegaCompletePartialOrder Î±]
    [OmegaCompletePartialOrder Î²] (f : Scott Î± â†’ Scott Î²) (hf : Continuous f) :
    OmegaCompletePartialOrder.Continuous' f := by
  have h : Monotone f := fun x y h â†¦ by
    have hf : IsUpperSet {x | Â¬f x â‰¤ f y} := ((notBelow_isOpen (f y)).preimage hf).isUpperSet
    simpa only [mem_setOf_eq, le_refl, not_true, imp_false, not_not] using hf h
  refine âŸ¨h, fun c â†¦ eq_of_forall_ge_iff fun z â†¦ ?_âŸ©
  -- âŠ¢ â†‘{ toFun := f, monotone' := h } (Ï‰Sup c) â‰¤ z â†” Ï‰Sup (Chain.map c { toFun :=  â€¦
  rcases (notBelow_isOpen z).preimage hf with âŸ¨hf, hf'âŸ©
  -- âŠ¢ â†‘{ toFun := f, monotone' := h } (Ï‰Sup c) â‰¤ z â†” Ï‰Sup (Chain.map c { toFun :=  â€¦
  specialize hf' c
  -- âŠ¢ â†‘{ toFun := f, monotone' := h } (Ï‰Sup c) â‰¤ z â†” Ï‰Sup (Chain.map c { toFun :=  â€¦
  simp only [OrderHom.coe_mk, mem_preimage, notBelow, mem_setOf_eq] at hf'
  -- âŠ¢ â†‘{ toFun := f, monotone' := h } (Ï‰Sup c) â‰¤ z â†” Ï‰Sup (Chain.map c { toFun :=  â€¦
  rw [â† not_iff_not]
  -- âŠ¢ Â¬â†‘{ toFun := f, monotone' := h } (Ï‰Sup c) â‰¤ z â†” Â¬Ï‰Sup (Chain.map c { toFun : â€¦
  simp only [Ï‰Sup_le_iff, hf', Ï‰Sup, iSup, sSup, mem_range, Chain.map_coe, Function.comp_apply,
    eq_iff_iff, not_forall, OrderHom.coe_mk]
  tauto
  -- ğŸ‰ no goals
#align Scott_continuous_of_continuous scottContinuous_of_continuous

theorem continuous_of_scottContinuous {Î± Î²} [OmegaCompletePartialOrder Î±]
    [OmegaCompletePartialOrder Î²] (f : Scott Î± â†’ Scott Î²)
    (hf : OmegaCompletePartialOrder.Continuous' f) : Continuous f := by
  rw [continuous_def]
  -- âŠ¢ âˆ€ (s : Set (Scott Î²)), IsOpen s â†’ IsOpen (f â»Â¹' s)
  intro s hs
  -- âŠ¢ IsOpen (f â»Â¹' s)
  change Continuous' (s âˆ˜ f)
  -- âŠ¢ Continuous' (s âˆ˜ f)
  cases' hs with hs hs'
  -- âŠ¢ Continuous' (s âˆ˜ f)
  cases' hf with hf hf'
  -- âŠ¢ Continuous' (s âˆ˜ f)
  apply Continuous.of_bundled
  -- âŠ¢ OmegaCompletePartialOrder.Continuous { toFun := s âˆ˜ f, monotone' := ?intro.i â€¦
  apply continuous_comp _ _ hf' hs'
  -- ğŸ‰ no goals
#align continuous_of_Scott_continuous continuous_of_scottContinuous
