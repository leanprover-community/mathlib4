/-
Copyright (c) 2023 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash, Bhavik Mehta, Daniel Weber, Stefan Kebekus
-/
import Mathlib.Tactic.TautoSet
import Mathlib.Topology.Constructions
import Mathlib.Data.Set.Subset
import Mathlib.Topology.Separation.Basic

/-!
# Discrete subsets of topological spaces

This file contains various additional properties of discrete subsets of topological spaces.

## Discreteness and compact sets

Given a topological space `X` together with a subset `s âŠ† X`, there are two distinct concepts of
"discreteness" which may hold. These are:
  (i) Every point of `s` is isolated (i.e., the subset topology induced on `s` is the discrete
      topology).
 (ii) Every compact subset of `X` meets `s` only finitely often (i.e., the inclusion map `s â†’ X`
      tends to the cocompact filter along the cofinite filter on `s`).

When `s` is closed, the two conditions are equivalent provided `X` is locally compact and T1,
see `IsClosed.tendsto_coe_cofinite_iff`.

### Main statements

* `tendsto_cofinite_cocompact_iff`:
* `IsClosed.tendsto_coe_cofinite_iff`:

## Co-discrete open sets

We define the filter `Filter.codiscreteWithin S`, which is the supremum of all `ğ“[S \ {x}] x`.
This is the filter of all open codiscrete sets within S. We also define `Filter.codiscrete` as
`Filter.codiscreteWithin univ`, which is the filter of all open codiscrete sets in the space.

-/

open Set Filter Function Topology

variable {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] {f : X â†’ Y}

theorem discreteTopology_subtype_iff {S : Set Y} :
    DiscreteTopology S â†” âˆ€ x âˆˆ S, ğ“[â‰ ] x âŠ“ ğ“Ÿ S = âŠ¥ := by
  simp_rw [discreteTopology_iff_nhds_ne, SetCoe.forall', nhds_ne_subtype_eq_bot_iff]

lemma discreteTopology_subtype_iff' {S : Set Y} :
    DiscreteTopology S â†” âˆ€ y âˆˆ S, âˆƒ U : Set Y, IsOpen U âˆ§ U âˆ© S = {y} := by
  simp [discreteTopology_iff_isOpen_singleton, isOpen_induced_iff, Set.ext_iff]
  grind

section cofinite_cocompact

omit [TopologicalSpace X] in
lemma tendsto_cofinite_cocompact_iff :
    Tendsto f cofinite (cocompact _) â†” âˆ€ K, IsCompact K â†’ Set.Finite (f â»Â¹' K) := by
  rw [hasBasis_cocompact.tendsto_right_iff]
  refine forallâ‚‚_congr (fun K _ â†¦ ?_)
  simp only [mem_compl_iff, eventually_cofinite, not_not, preimage]

lemma Continuous.discrete_of_tendsto_cofinite_cocompact [T1Space X] [WeaklyLocallyCompactSpace Y]
    (hf' : Continuous f) (hf : Tendsto f cofinite (cocompact _)) :
    DiscreteTopology X := by
  refine discreteTopology_iff_isOpen_singleton.mpr (fun x â†¦ ?_)
  obtain âŸ¨K : Set Y, hK : IsCompact K, hK' : K âˆˆ ğ“ (f x)âŸ© := exists_compact_mem_nhds (f x)
  obtain âŸ¨U : Set Y, hUâ‚ : U âŠ† K, hUâ‚‚ : IsOpen U, hUâ‚ƒ : f x âˆˆ UâŸ© := mem_nhds_iff.mp hK'
  have hUâ‚„ : Set.Finite (fâ»Â¹' U) :=
    Finite.subset (tendsto_cofinite_cocompact_iff.mp hf K hK) (preimage_mono hUâ‚)
  exact isOpen_singleton_of_finite_mem_nhds _ ((hUâ‚‚.preimage hf').mem_nhds hUâ‚ƒ) hUâ‚„

lemma tendsto_cofinite_cocompact_of_discrete [DiscreteTopology X]
    (hf : Tendsto f (cocompact _) (cocompact _)) :
    Tendsto f cofinite (cocompact _) := by
  convert hf
  rw [cocompact_eq_cofinite X]

lemma IsClosed.tendsto_coe_cofinite_of_discreteTopology
    {s : Set X} (hs : IsClosed s) (_hs' : DiscreteTopology s) :
    Tendsto ((â†‘) : s â†’ X) cofinite (cocompact _) :=
  tendsto_cofinite_cocompact_of_discrete hs.isClosedEmbedding_subtypeVal.tendsto_cocompact

lemma IsClosed.tendsto_coe_cofinite_iff [T1Space X] [WeaklyLocallyCompactSpace X]
    {s : Set X} (hs : IsClosed s) :
    Tendsto ((â†‘) : s â†’ X) cofinite (cocompact _) â†” DiscreteTopology s :=
  âŸ¨continuous_subtype_val.discrete_of_tendsto_cofinite_cocompact,
   fun _ â†¦ hs.tendsto_coe_cofinite_of_discreteTopology inferInstanceâŸ©

end cofinite_cocompact

section codiscrete_filter

/-- Criterion for a subset `S âŠ† X` to be closed and discrete in terms of the punctured
neighbourhood filter at an arbitrary point of `X`. (Compare `discreteTopology_subtype_iff`.) -/
theorem isClosed_and_discrete_iff {S : Set X} :
    IsClosed S âˆ§ DiscreteTopology S â†” âˆ€ x, Disjoint (ğ“[â‰ ] x) (ğ“Ÿ S) := by
  rw [discreteTopology_subtype_iff, isClosed_iff_clusterPt, â† forall_and]
  congrm (âˆ€ x, ?_)
  rw [â† not_imp_not, clusterPt_iff_not_disjoint, not_not, â† disjoint_iff]
  constructor <;> intro H
  Â· by_cases hx : x âˆˆ S
    exacts [H.2 hx, (H.1 hx).mono_left nhdsWithin_le_nhds]
  Â· refine âŸ¨fun hx â†¦ ?_, fun _ â†¦ HâŸ©
    simpa [disjoint_iff, nhdsWithin, inf_assoc, hx] using H

/-- The filter of sets with no accumulation points inside a set `S : Set X`, implemented
as the supremum over all punctured neighborhoods within `S`. -/
def Filter.codiscreteWithin (S : Set X) : Filter X := â¨† x âˆˆ S, ğ“[S \ {x}] x

lemma mem_codiscreteWithin {S T : Set X} :
    S âˆˆ codiscreteWithin T â†” âˆ€ x âˆˆ T, Disjoint (ğ“[â‰ ] x) (ğ“Ÿ (T \ S)) := by
  simp only [codiscreteWithin, mem_iSup, mem_nhdsWithin, disjoint_principal_right, subset_def,
    mem_diff, mem_inter_iff, mem_compl_iff]
  congr! 7 with x - u y
  tauto

/--
A set `s` is codiscrete within `U` iff `s âˆª Uá¶œ` is a punctured neighborhood of every point in `U`.
-/
theorem mem_codiscreteWithin_iff_forall_mem_nhdsNE {S T : Set X} :
    S âˆˆ codiscreteWithin T â†” âˆ€ x âˆˆ T, S âˆª Tá¶œ âˆˆ ğ“[â‰ ] x := by
  simp_rw [mem_codiscreteWithin, disjoint_principal_right, Set.compl_diff]

lemma mem_codiscreteWithin_accPt {S T : Set X} :
    S âˆˆ codiscreteWithin T â†” âˆ€ x âˆˆ T, Â¬AccPt x (ğ“Ÿ (T \ S)) := by
  simp only [mem_codiscreteWithin, disjoint_iff, AccPt, not_neBot]

/-- Any set is codiscrete within itself. -/
@[simp]
theorem Filter.self_mem_codiscreteWithin (U : Set X) :
    U âˆˆ Filter.codiscreteWithin U := by simp [mem_codiscreteWithin]

/-- If a set is codiscrete within `U`, then it is codiscrete within any subset of `U`. -/
lemma Filter.codiscreteWithin.mono {Uâ‚ U : Set X} (hU : Uâ‚ âŠ† U) :
    codiscreteWithin Uâ‚ â‰¤ codiscreteWithin U := by
  refine (biSup_mono hU).trans <| iSupâ‚‚_mono fun _ _ â†¦ ?_
  gcongr

/-- If `s` is codiscrete within `U`, then `sá¶œ âˆ© U` has discrete topology. -/
theorem discreteTopology_of_codiscreteWithin {U s : Set X} (h : s âˆˆ Filter.codiscreteWithin U) :
    DiscreteTopology ((sá¶œ âˆ© U) : Set X) := by
  rw [(by simp : ((sá¶œ âˆ© U) : Set X) = ((s âˆª Uá¶œ)á¶œ : Set X)), discreteTopology_subtype_iff]
  simp_rw [mem_codiscreteWithin, Filter.disjoint_principal_right] at h
  intro x hx
  rw [â† Filter.mem_iff_inf_principal_compl, â† Set.compl_diff]
  simp_all only [h x, Set.compl_union, compl_compl, Set.mem_inter_iff, Set.mem_compl_iff]

/-- Helper lemma for `codiscreteWithin_iff_locallyFiniteComplementWithin`: A set `s` is
`codiscreteWithin U` iff every point `z âˆˆ U` has a punctured neighborhood that does not intersect
`U \ s`. -/
lemma codiscreteWithin_iff_locallyEmptyComplementWithin {s U : Set X} :
    s âˆˆ codiscreteWithin U â†” âˆ€ z âˆˆ U, âˆƒ t âˆˆ ğ“[â‰ ] z, t âˆ© (U \ s) = âˆ… := by
  simp only [mem_codiscreteWithin, disjoint_principal_right]
  refine âŸ¨fun h z hz â†¦ âŸ¨(U \ s)á¶œ, h z hz, by simpâŸ©, fun h z hz â†¦ ?_âŸ©
  rw [â† exists_mem_subset_iff]
  obtain âŸ¨t, hâ‚t, hâ‚‚tâŸ© := h z hz
  use t, hâ‚t, (disjoint_iff_inter_eq_empty.mpr hâ‚‚t).subset_compl_right

/-- If `U` is closed and `s` is codiscrete within `U`, then `U \ s` is closed. -/
theorem isClosed_sdiff_of_codiscreteWithin {s U : Set X} (hs : s âˆˆ codiscreteWithin U)
    (hU : IsClosed U) :
    IsClosed (U \ s) := by
  rw [â† isOpen_compl_iff, isOpen_iff_eventually]
  intro x hx
  by_cases hâ‚x : x âˆˆ U
  Â· rw [mem_codiscreteWithin] at hs
    filter_upwards [eventually_nhdsWithin_iff.1 (disjoint_principal_right.1 (hs x hâ‚x))]
    intro a ha
    by_cases hâ‚‚a : a = x
    Â· tauto_set
    Â· specialize ha hâ‚‚a
      tauto_set
  Â· rw [eventually_iff_exists_mem]
    use Uá¶œ, hU.compl_mem_nhds hâ‚x
    intro y hy
    tauto_set

/-- In a T1Space, punctured neighborhoods are stable under removing finite sets of points. -/
theorem nhdsNE_of_nhdsNE_sdiff_finite {X : Type*} [TopologicalSpace X] [T1Space X] {x : X}
    {U s : Set X} (hU : U âˆˆ ğ“[â‰ ] x) (hs : Finite s) :
    U \ s âˆˆ ğ“[â‰ ] x := by
  rw [mem_nhdsWithin] at hU âŠ¢
  obtain âŸ¨t, ht, hâ‚ts, hâ‚‚tsâŸ© := hU
  use t \ (s \ {x})
  constructor
  Â· rw [â† isClosed_compl_iff, compl_diff]
    exact s.toFinite.diff.isClosed.union (isClosed_compl_iff.2 ht)
  Â· tauto_set

@[deprecated (since := "2025-05-22")]
alias nhdNE_of_nhdNE_sdiff_finite := nhdsNE_of_nhdsNE_sdiff_finite

/-- In a T1Space, a set `s` is codiscreteWithin `U` iff it has locally finite complement within `U`.
More precisely: `s` is codiscreteWithin `U` iff every point `z âˆˆ U` has a punctured neighborhood
intersect `U \ s` in only finitely many points. -/
theorem codiscreteWithin_iff_locallyFiniteComplementWithin [T1Space X] {s U : Set X} :
    s âˆˆ codiscreteWithin U â†” âˆ€ z âˆˆ U, âˆƒ t âˆˆ ğ“ z, Set.Finite (t âˆ© (U \ s)) := by
  rw [codiscreteWithin_iff_locallyEmptyComplementWithin]
  constructor
  Â· intro h z hâ‚z
    obtain âŸ¨t, hâ‚t, hâ‚‚tâŸ© := h z hâ‚z
    use insert z t, insert_mem_nhds_iff.mpr hâ‚t
    by_cases hz : z âˆˆ U \ s
    Â· rw [inter_comm, inter_insert_of_mem hz, inter_comm, hâ‚‚t]
      simp
    Â· rw [inter_comm, inter_insert_of_notMem hz, inter_comm, hâ‚‚t]
      simp
  Â· intro h z hâ‚z
    obtain âŸ¨t, hâ‚t, hâ‚‚tâŸ© := h z hâ‚z
    use t \ (t âˆ© (U \ s)), nhdsNE_of_nhdsNE_sdiff_finite (mem_nhdsWithin_of_mem_nhds hâ‚t) hâ‚‚t
    simp

/-- In any topological space, the open sets with discrete complement form a filter,
defined as the supremum of all punctured neighborhoods.

See `Filter.mem_codiscrete'` for the equivalence. -/
def Filter.codiscrete (X : Type*) [TopologicalSpace X] : Filter X := codiscreteWithin Set.univ

lemma mem_codiscrete {S : Set X} :
    S âˆˆ codiscrete X â†” âˆ€ x, Disjoint (ğ“[â‰ ] x) (ğ“Ÿ Sá¶œ) := by
  simp [codiscrete, mem_codiscreteWithin, compl_eq_univ_diff]

lemma mem_codiscrete_accPt {S : Set X} :
    S âˆˆ codiscrete X â†” âˆ€ x, Â¬AccPt x (ğ“Ÿ Sá¶œ) := by
  simp only [mem_codiscrete, disjoint_iff, AccPt, not_neBot]

lemma mem_codiscrete' {S : Set X} :
    S âˆˆ codiscrete X â†” IsOpen S âˆ§ DiscreteTopology â†‘Sá¶œ := by
  rw [mem_codiscrete, â† isClosed_compl_iff, isClosed_and_discrete_iff]

lemma mem_codiscrete_subtype_iff_mem_codiscreteWithin {S : Set X} {U : Set S} :
    U âˆˆ codiscrete S â†” (â†‘) '' U âˆˆ codiscreteWithin S := by
  simp only [mem_codiscrete, disjoint_principal_right, compl_compl, Subtype.forall,
    mem_codiscreteWithin]
  congr! with x hx
  constructor
  Â· rw [nhdsWithin_subtype, mem_comap]
    rintro âŸ¨t, ht1, ht2âŸ©
    rw [mem_nhdsWithin] at ht1 âŠ¢
    obtain âŸ¨u, hu1, hu2, hu3âŸ© := ht1
    refine âŸ¨u, hu1, hu2, fun v hv â†¦ ?_âŸ©
    simpa using fun hv2 â†¦ âŸ¨hv2, ht2 <| hu3 <| by simpa [hv2]âŸ©
  Â· suffices Tendsto (â†‘) (ğ“[â‰ ] (âŸ¨x, hxâŸ© : S)) (ğ“[â‰ ] x) by convert tendsto_def.mp this _; ext; simp
    exact tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within _
      continuous_subtype_val.continuousWithinAt <| eventually_mem_nhdsWithin.mono (by simp)

end codiscrete_filter

section discrete_union

lemma compl_mem_codiscrete_iff {S : Set X} :
    Sá¶œ âˆˆ codiscrete X â†” IsClosed S âˆ§ DiscreteTopology â†‘S := by
  rw [mem_codiscrete, compl_compl, isClosed_and_discrete_iff]

/-- The union of two discrete closed subsets is discrete. -/
theorem discreteTopology_union {S T : Set X} (hs : DiscreteTopology S) (ht : DiscreteTopology T)
    (hs' : IsClosed S) (ht' : IsClosed T) : DiscreteTopology â†‘(S âˆª T) := by
  suffices (S âˆª T)á¶œ âˆˆ codiscrete X from compl_mem_codiscrete_iff.mp this |>.2
  have hS : Sá¶œ âˆˆ codiscrete X := by simpa [compl_mem_codiscrete_iff] using âŸ¨hs', hsâŸ©
  have hT : Tá¶œ âˆˆ codiscrete X := by simpa [compl_mem_codiscrete_iff] using âŸ¨ht', htâŸ©
  simpa using inter_mem hS hT

/-- The union of finitely many discrete closed subsets is discrete. -/
theorem discreteTopology_biUnion_finset {Î¹ : Type*} {I : Finset Î¹} {s : Î¹ â†’ Set X}
    (hs : âˆ€ i âˆˆ I, DiscreteTopology (s i)) (hs' : âˆ€ i âˆˆ I, IsClosed (s i)) :
    DiscreteTopology (â‹ƒ i âˆˆ I, s i) := by
  suffices (â‹ƒ i âˆˆ I, s i)á¶œ âˆˆ codiscrete X from (compl_mem_codiscrete_iff.mp this).2
  simpa [biInter_finset_mem I] using fun i hi â†¦ compl_mem_codiscrete_iff.mpr âŸ¨hs' i hi, hs i hiâŸ©

/-- The union of finitely many discrete closed subsets is discrete. -/
theorem discreteTopology_iUnion_fintype {Î¹ : Type*} [Fintype Î¹] {s : Î¹ â†’ Set X}
    (hs : âˆ€ i, DiscreteTopology (s i)) (hs' : âˆ€ i, IsClosed (s i)) :
    DiscreteTopology (â‹ƒ i, s i) := by
  convert discreteTopology_biUnion_finset (I := .univ) (fun i _ â†¦ hs i) (fun i _ â†¦ hs' i) <;>
    simp

end discrete_union
