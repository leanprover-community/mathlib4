/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Patrick Massot
-/
import Mathlib.Topology.Maps.Basic
import Mathlib.Topology.NhdsSet
import Mathlib.Order.Filter.Curry

/-!
# Constructions of new topological spaces from old ones

This file constructs products, sums, subtypes and quotients of topological spaces
and sets up their basic theory, such as criteria for maps into or out of these
constructions to be continuous; descriptions of the open sets, neighborhood filters,
and generators of these constructions; and their behavior with respect to embeddings
and other specific classes of maps.

## Implementation note

The constructed topologies are defined using induced and coinduced topologies
along with the complete lattice structure on topologies. Their universal properties
(for example, a map `X â†’ Y Ã— Z` is continuous if and only if both projections
`X â†’ Y`, `X â†’ Z` are) follow easily using order-theoretic descriptions of
continuity. With more work we can also extract descriptions of the open sets,
neighborhood filters and so on.

## Tags

product, sum, disjoint union, subspace, quotient space

-/

noncomputable section

open Topology TopologicalSpace Set Filter Function

universe u v

variable {X : Type u} {Y : Type v} {Z W Îµ Î¶ : Type*}

section Constructions

instance {r : X â†’ X â†’ Prop} [t : TopologicalSpace X] : TopologicalSpace (Quot r) :=
  coinduced (Quot.mk r) t

instance instTopologicalSpaceQuotient {s : Setoid X} [t : TopologicalSpace X] :
    TopologicalSpace (Quotient s) :=
  coinduced Quotient.mk' t

instance instTopologicalSpaceProd [tâ‚ : TopologicalSpace X] [tâ‚‚ : TopologicalSpace Y] :
    TopologicalSpace (X Ã— Y) :=
  induced Prod.fst tâ‚ âŠ“ induced Prod.snd tâ‚‚

instance instTopologicalSpaceSum [tâ‚ : TopologicalSpace X] [tâ‚‚ : TopologicalSpace Y] :
    TopologicalSpace (X âŠ• Y) :=
  coinduced Sum.inl tâ‚ âŠ” coinduced Sum.inr tâ‚‚

instance instTopologicalSpaceSigma {Î¹ : Type*} {X : Î¹ â†’ Type v} [tâ‚‚ : âˆ€ i, TopologicalSpace (X i)] :
    TopologicalSpace (Sigma X) :=
  â¨† i, coinduced (Sigma.mk i) (tâ‚‚ i)

instance Pi.topologicalSpace {Î¹ : Type*} {Y : Î¹ â†’ Type v} [tâ‚‚ : (i : Î¹) â†’ TopologicalSpace (Y i)] :
    TopologicalSpace ((i : Î¹) â†’ Y i) :=
  â¨… i, induced (fun f => f i) (tâ‚‚ i)

instance ULift.topologicalSpace [t : TopologicalSpace X] : TopologicalSpace (ULift.{v, u} X) :=
  t.induced ULift.down

/-!
### `Additive`, `Multiplicative`

The topology on those type synonyms is inherited without change.
-/

section

variable [TopologicalSpace X]

open Additive Multiplicative

instance : TopologicalSpace (Additive X) := â€¹TopologicalSpace Xâ€º
instance : TopologicalSpace (Multiplicative X) := â€¹TopologicalSpace Xâ€º

instance [DiscreteTopology X] : DiscreteTopology (Additive X) := â€¹DiscreteTopology Xâ€º
instance [DiscreteTopology X] : DiscreteTopology (Multiplicative X) := â€¹DiscreteTopology Xâ€º

theorem continuous_ofMul : Continuous (ofMul : X â†’ Additive X) := continuous_id

theorem continuous_toMul : Continuous (toMul : Additive X â†’ X) := continuous_id

theorem continuous_ofAdd : Continuous (ofAdd : X â†’ Multiplicative X) := continuous_id

theorem continuous_toAdd : Continuous (toAdd : Multiplicative X â†’ X) := continuous_id

theorem isOpenMap_ofMul : IsOpenMap (ofMul : X â†’ Additive X) := IsOpenMap.id

theorem isOpenMap_toMul : IsOpenMap (toMul : Additive X â†’ X) := IsOpenMap.id

theorem isOpenMap_ofAdd : IsOpenMap (ofAdd : X â†’ Multiplicative X) := IsOpenMap.id

theorem isOpenMap_toAdd : IsOpenMap (toAdd : Multiplicative X â†’ X) := IsOpenMap.id

theorem isClosedMap_ofMul : IsClosedMap (ofMul : X â†’ Additive X) := IsClosedMap.id

theorem isClosedMap_toMul : IsClosedMap (toMul : Additive X â†’ X) := IsClosedMap.id

theorem isClosedMap_ofAdd : IsClosedMap (ofAdd : X â†’ Multiplicative X) := IsClosedMap.id

theorem isClosedMap_toAdd : IsClosedMap (toAdd : Multiplicative X â†’ X) := IsClosedMap.id

theorem nhds_ofMul (x : X) : ğ“ (ofMul x) = map ofMul (ğ“ x) := rfl

theorem nhds_ofAdd (x : X) : ğ“ (ofAdd x) = map ofAdd (ğ“ x) := rfl

theorem nhds_toMul (x : Additive X) : ğ“ (toMul x) = map toMul (ğ“ x) := rfl

theorem nhds_toAdd (x : Multiplicative X) : ğ“ (toAdd x) = map toAdd (ğ“ x) := rfl

end

/-!
### Order dual

The topology on this type synonym is inherited without change.
-/


section

variable [TopologicalSpace X]

open OrderDual

instance OrderDual.instTopologicalSpace : TopologicalSpace Xáµ’áµˆ := â€¹_â€º
instance OrderDual.instDiscreteTopology [DiscreteTopology X] : DiscreteTopology Xáµ’áµˆ := â€¹_â€º

theorem continuous_toDual : Continuous (toDual : X â†’ Xáµ’áµˆ) := continuous_id

theorem continuous_ofDual : Continuous (ofDual : Xáµ’áµˆ â†’ X) := continuous_id

theorem isOpenMap_toDual : IsOpenMap (toDual : X â†’ Xáµ’áµˆ) := IsOpenMap.id

theorem isOpenMap_ofDual : IsOpenMap (ofDual : Xáµ’áµˆ â†’ X) := IsOpenMap.id

theorem isClosedMap_toDual : IsClosedMap (toDual : X â†’ Xáµ’áµˆ) := IsClosedMap.id

theorem isClosedMap_ofDual : IsClosedMap (ofDual : Xáµ’áµˆ â†’ X) := IsClosedMap.id

theorem nhds_toDual (x : X) : ğ“ (toDual x) = map toDual (ğ“ x) := rfl

theorem nhds_ofDual (x : X) : ğ“ (ofDual x) = map ofDual (ğ“ x) := rfl

variable [Preorder X] {x : X}

instance OrderDual.instNeBotNhdsWithinIoi [(ğ“[<] x).NeBot] : (ğ“[>] toDual x).NeBot := â€¹_â€º
instance OrderDual.instNeBotNhdsWithinIio [(ğ“[>] x).NeBot] : (ğ“[<] toDual x).NeBot := â€¹_â€º

end

theorem Quotient.preimage_mem_nhds [TopologicalSpace X] [s : Setoid X] {V : Set <| Quotient s}
    {x : X} (hs : V âˆˆ ğ“ (Quotient.mk' x)) : Quotient.mk' â»Â¹' V âˆˆ ğ“ x :=
  preimage_nhds_coinduced hs

/-- The image of a dense set under `Quotient.mk'` is a dense set. -/
theorem Dense.quotient [Setoid X] [TopologicalSpace X] {s : Set X} (H : Dense s) :
    Dense (Quotient.mk' '' s) :=
  Quotient.surjective_Quotient_mk''.denseRange.dense_image continuous_coinduced_rng H

/-- The composition of `Quotient.mk'` and a function with dense range has dense range. -/
theorem DenseRange.quotient [Setoid X] [TopologicalSpace X] {f : Y â†’ X} (hf : DenseRange f) :
    DenseRange (Quotient.mk' âˆ˜ f) :=
  Quotient.surjective_Quotient_mk''.denseRange.comp hf continuous_coinduced_rng

theorem continuous_map_of_le {Î± : Type*} [TopologicalSpace Î±]
    {s t : Setoid Î±} (h : s â‰¤ t) : Continuous (Setoid.map_of_le h) :=
  continuous_coinduced_rng

theorem continuous_map_sInf {Î± : Type*} [TopologicalSpace Î±]
    {S : Set (Setoid Î±)} {s : Setoid Î±} (h : s âˆˆ S) : Continuous (Setoid.map_sInf h) :=
  continuous_coinduced_rng

instance {p : X â†’ Prop} [TopologicalSpace X] [DiscreteTopology X] : DiscreteTopology (Subtype p) :=
  âŸ¨bot_unique fun s _ => âŸ¨(â†‘) '' s, isOpen_discrete _, preimage_image_eq _ Subtype.val_injectiveâŸ©âŸ©

instance Sum.discreteTopology [TopologicalSpace X] [TopologicalSpace Y] [h : DiscreteTopology X]
    [hY : DiscreteTopology Y] : DiscreteTopology (X âŠ• Y) :=
  âŸ¨sup_eq_bot_iff.2 <| by simp [h.eq_bot, hY.eq_bot]âŸ©

instance Sigma.discreteTopology {Î¹ : Type*} {Y : Î¹ â†’ Type v} [âˆ€ i, TopologicalSpace (Y i)]
    [h : âˆ€ i, DiscreteTopology (Y i)] : DiscreteTopology (Sigma Y) :=
  âŸ¨iSup_eq_bot.2 fun _ => by simp only [(h _).eq_bot, coinduced_bot]âŸ©

@[simp] lemma comap_nhdsWithin_range {Î± Î²} [TopologicalSpace Î²] (f : Î± â†’ Î²) (y : Î²) :
    comap f (ğ“[range f] y) = comap f (ğ“ y) := comap_inf_principal_range

section Top

variable [TopologicalSpace X]

/-
The ğ“ filter and the subspace topology.
-/
theorem mem_nhds_subtype (s : Set X) (x : { x // x âˆˆ s }) (t : Set { x // x âˆˆ s }) :
    t âˆˆ ğ“ x â†” âˆƒ u âˆˆ ğ“ (x : X), Subtype.val â»Â¹' u âŠ† t :=
  mem_nhds_induced _ x t

theorem nhds_subtype (s : Set X) (x : { x // x âˆˆ s }) : ğ“ x = comap (â†‘) (ğ“ (x : X)) :=
  nhds_induced _ x

lemma nhds_subtype_eq_comap_nhdsWithin (s : Set X) (x : { x // x âˆˆ s }) :
    ğ“ x = comap (â†‘) (ğ“[s] (x : X)) := by
  rw [nhds_subtype, â† comap_nhdsWithin_range, Subtype.range_val]

theorem nhdsWithin_subtype_eq_bot_iff {s t : Set X} {x : s} :
    ğ“[((â†‘) : s â†’ X) â»Â¹' t] x = âŠ¥ â†” ğ“[t] (x : X) âŠ“ ğ“Ÿ s = âŠ¥ := by
  rw [inf_principal_eq_bot_iff_comap, nhdsWithin, nhdsWithin, comap_inf, comap_principal,
    nhds_induced]

theorem nhds_ne_subtype_eq_bot_iff {S : Set X} {x : S} :
    ğ“[â‰ ] x = âŠ¥ â†” ğ“[â‰ ] (x : X) âŠ“ ğ“Ÿ S = âŠ¥ := by
  rw [â† nhdsWithin_subtype_eq_bot_iff, preimage_compl, â† image_singleton,
    Subtype.coe_injective.preimage_image]

theorem nhds_ne_subtype_neBot_iff {S : Set X} {x : S} :
    (ğ“[â‰ ] x).NeBot â†” (ğ“[â‰ ] (x : X) âŠ“ ğ“Ÿ S).NeBot := by
  rw [neBot_iff, neBot_iff, not_iff_not, nhds_ne_subtype_eq_bot_iff]

theorem discreteTopology_subtype_iff {S : Set X} :
    DiscreteTopology S â†” âˆ€ x âˆˆ S, ğ“[â‰ ] x âŠ“ ğ“Ÿ S = âŠ¥ := by
  simp_rw [discreteTopology_iff_nhds_ne, SetCoe.forall', nhds_ne_subtype_eq_bot_iff]

end Top

/-- A type synonym equipped with the topology whose open sets are the empty set and the sets with
finite complements. -/
def CofiniteTopology (X : Type*) := X

namespace CofiniteTopology

/-- The identity equivalence between `` and `CofiniteTopology `. -/
def of : X â‰ƒ CofiniteTopology X :=
  Equiv.refl X

instance [Inhabited X] : Inhabited (CofiniteTopology X) where default := of default

instance : TopologicalSpace (CofiniteTopology X) where
  IsOpen s := s.Nonempty â†’ Set.Finite sá¶œ
  isOpen_univ := by simp
  isOpen_inter s t := by
    rintro hs ht âŸ¨x, hxs, hxtâŸ©
    rw [compl_inter]
    exact (hs âŸ¨x, hxsâŸ©).union (ht âŸ¨x, hxtâŸ©)
  isOpen_sUnion := by
    rintro s h âŸ¨x, t, hts, hztâŸ©
    rw [compl_sUnion]
    exact Finite.sInter (mem_image_of_mem _ hts) (h t hts âŸ¨x, hztâŸ©)

theorem isOpen_iff {s : Set (CofiniteTopology X)} : IsOpen s â†” s.Nonempty â†’ sá¶œ.Finite :=
  Iff.rfl

theorem isOpen_iff' {s : Set (CofiniteTopology X)} : IsOpen s â†” s = âˆ… âˆ¨ sá¶œ.Finite := by
  simp only [isOpen_iff, nonempty_iff_ne_empty, or_iff_not_imp_left]

theorem isClosed_iff {s : Set (CofiniteTopology X)} : IsClosed s â†” s = univ âˆ¨ s.Finite := by
  simp only [â† isOpen_compl_iff, isOpen_iff', compl_compl, compl_empty_iff]

theorem nhds_eq (x : CofiniteTopology X) : ğ“ x = pure x âŠ” cofinite := by
  ext U
  rw [mem_nhds_iff]
  constructor
  Â· rintro âŸ¨V, hVU, V_op, haVâŸ©
    exact mem_sup.mpr âŸ¨hVU haV, mem_of_superset (V_op âŸ¨_, haVâŸ©) hVUâŸ©
  Â· rintro âŸ¨hU : x âˆˆ U, hU' : Uá¶œ.FiniteâŸ©
    exact âŸ¨U, Subset.rfl, fun _ => hU', hUâŸ©

theorem mem_nhds_iff {x : CofiniteTopology X} {s : Set (CofiniteTopology X)} :
    s âˆˆ ğ“ x â†” x âˆˆ s âˆ§ sá¶œ.Finite := by simp [nhds_eq]

end CofiniteTopology

end Constructions

section Prod

variable [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z] [TopologicalSpace W]
  [TopologicalSpace Îµ] [TopologicalSpace Î¶]

-- Porting note (#11215): TODO: Lean 4 fails to deduce implicit args
@[simp] theorem continuous_prod_mk {f : X â†’ Y} {g : X â†’ Z} :
    (Continuous fun x => (f x, g x)) â†” Continuous f âˆ§ Continuous g :=
  (@continuous_inf_rng X (Y Ã— Z) _ _ (TopologicalSpace.induced Prod.fst _)
    (TopologicalSpace.induced Prod.snd _)).trans <|
    continuous_induced_rng.and continuous_induced_rng

@[continuity]
theorem continuous_fst : Continuous (@Prod.fst X Y) :=
  (continuous_prod_mk.1 continuous_id).1

/-- Postcomposing `f` with `Prod.fst` is continuous -/
@[fun_prop]
theorem Continuous.fst {f : X â†’ Y Ã— Z} (hf : Continuous f) : Continuous fun x : X => (f x).1 :=
  continuous_fst.comp hf

/-- Precomposing `f` with `Prod.fst` is continuous -/
theorem Continuous.fst' {f : X â†’ Z} (hf : Continuous f) : Continuous fun x : X Ã— Y => f x.fst :=
  hf.comp continuous_fst

theorem continuousAt_fst {p : X Ã— Y} : ContinuousAt Prod.fst p :=
  continuous_fst.continuousAt

/-- Postcomposing `f` with `Prod.fst` is continuous at `x` -/
@[fun_prop]
theorem ContinuousAt.fst {f : X â†’ Y Ã— Z} {x : X} (hf : ContinuousAt f x) :
    ContinuousAt (fun x : X => (f x).1) x :=
  continuousAt_fst.comp hf

/-- Precomposing `f` with `Prod.fst` is continuous at `(x, y)` -/
theorem ContinuousAt.fst' {f : X â†’ Z} {x : X} {y : Y} (hf : ContinuousAt f x) :
    ContinuousAt (fun x : X Ã— Y => f x.fst) (x, y) :=
  ContinuousAt.comp hf continuousAt_fst

/-- Precomposing `f` with `Prod.fst` is continuous at `x : X Ã— Y` -/
theorem ContinuousAt.fst'' {f : X â†’ Z} {x : X Ã— Y} (hf : ContinuousAt f x.fst) :
    ContinuousAt (fun x : X Ã— Y => f x.fst) x :=
  hf.comp continuousAt_fst

theorem Filter.Tendsto.fst_nhds {X} {l : Filter X} {f : X â†’ Y Ã— Z} {p : Y Ã— Z}
    (h : Tendsto f l (ğ“ p)) : Tendsto (fun a â†¦ (f a).1) l (ğ“ <| p.1) :=
  continuousAt_fst.tendsto.comp h

@[continuity]
theorem continuous_snd : Continuous (@Prod.snd X Y) :=
  (continuous_prod_mk.1 continuous_id).2

/-- Postcomposing `f` with `Prod.snd` is continuous -/
@[fun_prop]
theorem Continuous.snd {f : X â†’ Y Ã— Z} (hf : Continuous f) : Continuous fun x : X => (f x).2 :=
  continuous_snd.comp hf

/-- Precomposing `f` with `Prod.snd` is continuous -/
theorem Continuous.snd' {f : Y â†’ Z} (hf : Continuous f) : Continuous fun x : X Ã— Y => f x.snd :=
  hf.comp continuous_snd

theorem continuousAt_snd {p : X Ã— Y} : ContinuousAt Prod.snd p :=
  continuous_snd.continuousAt

/-- Postcomposing `f` with `Prod.snd` is continuous at `x` -/
@[fun_prop]
theorem ContinuousAt.snd {f : X â†’ Y Ã— Z} {x : X} (hf : ContinuousAt f x) :
    ContinuousAt (fun x : X => (f x).2) x :=
  continuousAt_snd.comp hf

/-- Precomposing `f` with `Prod.snd` is continuous at `(x, y)` -/
theorem ContinuousAt.snd' {f : Y â†’ Z} {x : X} {y : Y} (hf : ContinuousAt f y) :
    ContinuousAt (fun x : X Ã— Y => f x.snd) (x, y) :=
  ContinuousAt.comp hf continuousAt_snd

/-- Precomposing `f` with `Prod.snd` is continuous at `x : X Ã— Y` -/
theorem ContinuousAt.snd'' {f : Y â†’ Z} {x : X Ã— Y} (hf : ContinuousAt f x.snd) :
    ContinuousAt (fun x : X Ã— Y => f x.snd) x :=
  hf.comp continuousAt_snd

theorem Filter.Tendsto.snd_nhds {X} {l : Filter X} {f : X â†’ Y Ã— Z} {p : Y Ã— Z}
    (h : Tendsto f l (ğ“ p)) : Tendsto (fun a â†¦ (f a).2) l (ğ“ <| p.2) :=
  continuousAt_snd.tendsto.comp h

@[continuity, fun_prop]
theorem Continuous.prod_mk {f : Z â†’ X} {g : Z â†’ Y} (hf : Continuous f) (hg : Continuous g) :
    Continuous fun x => (f x, g x) :=
  continuous_prod_mk.2 âŸ¨hf, hgâŸ©

@[continuity]
theorem Continuous.Prod.mk (x : X) : Continuous fun y : Y => (x, y) :=
  continuous_const.prod_mk continuous_id

@[continuity]
theorem Continuous.Prod.mk_left (y : Y) : Continuous fun x : X => (x, y) :=
  continuous_id.prod_mk continuous_const

/-- If `f x y` is continuous in `x` for all `y âˆˆ s`,
then the set of `x` such that `f x` maps `s` to `t` is closed. -/
lemma IsClosed.setOf_mapsTo {Î± : Type*} {f : X â†’ Î± â†’ Z} {s : Set Î±} {t : Set Z} (ht : IsClosed t)
    (hf : âˆ€ a âˆˆ s, Continuous (f Â· a)) : IsClosed {x | MapsTo (f x) s t} := by
  simpa only [MapsTo, setOf_forall] using isClosed_biInter fun y hy â†¦ ht.preimage (hf y hy)

theorem Continuous.compâ‚‚ {g : X Ã— Y â†’ Z} (hg : Continuous g) {e : W â†’ X} (he : Continuous e)
    {f : W â†’ Y} (hf : Continuous f) : Continuous fun w => g (e w, f w) :=
  hg.comp <| he.prod_mk hf

theorem Continuous.compâ‚ƒ {g : X Ã— Y Ã— Z â†’ Îµ} (hg : Continuous g) {e : W â†’ X} (he : Continuous e)
    {f : W â†’ Y} (hf : Continuous f) {k : W â†’ Z} (hk : Continuous k) :
    Continuous fun w => g (e w, f w, k w) :=
  hg.compâ‚‚ he <| hf.prod_mk hk

theorem Continuous.compâ‚„ {g : X Ã— Y Ã— Z Ã— Î¶ â†’ Îµ} (hg : Continuous g) {e : W â†’ X} (he : Continuous e)
    {f : W â†’ Y} (hf : Continuous f) {k : W â†’ Z} (hk : Continuous k) {l : W â†’ Î¶}
    (hl : Continuous l) : Continuous fun w => g (e w, f w, k w, l w) :=
  hg.compâ‚ƒ he hf <| hk.prod_mk hl

@[continuity]
theorem Continuous.prod_map {f : Z â†’ X} {g : W â†’ Y} (hf : Continuous f) (hg : Continuous g) :
    Continuous fun p : Z Ã— W => (f p.1, g p.2) :=
  hf.fst'.prod_mk hg.snd'

/-- A version of `continuous_inf_dom_left` for binary functions -/
theorem continuous_inf_dom_leftâ‚‚ {X Y Z} {f : X â†’ Y â†’ Z} {ta1 ta2 : TopologicalSpace X}
    {tb1 tb2 : TopologicalSpace Y} {tc1 : TopologicalSpace Z}
    (h : by haveI := ta1; haveI := tb1; exact Continuous fun p : X Ã— Y => f p.1 p.2) : by
    haveI := ta1 âŠ“ ta2; haveI := tb1 âŠ“ tb2; exact Continuous fun p : X Ã— Y => f p.1 p.2 := by
  have ha := @continuous_inf_dom_left _ _ id ta1 ta2 ta1 (@continuous_id _ (id _))
  have hb := @continuous_inf_dom_left _ _ id tb1 tb2 tb1 (@continuous_id _ (id _))
  have h_continuous_id := @Continuous.prod_map _ _ _ _ ta1 tb1 (ta1 âŠ“ ta2) (tb1 âŠ“ tb2) _ _ ha hb
  exact @Continuous.comp _ _ _ (id _) (id _) _ _ _ h h_continuous_id

/-- A version of `continuous_inf_dom_right` for binary functions -/
theorem continuous_inf_dom_rightâ‚‚ {X Y Z} {f : X â†’ Y â†’ Z} {ta1 ta2 : TopologicalSpace X}
    {tb1 tb2 : TopologicalSpace Y} {tc1 : TopologicalSpace Z}
    (h : by haveI := ta2; haveI := tb2; exact Continuous fun p : X Ã— Y => f p.1 p.2) : by
    haveI := ta1 âŠ“ ta2; haveI := tb1 âŠ“ tb2; exact Continuous fun p : X Ã— Y => f p.1 p.2 := by
  have ha := @continuous_inf_dom_right _ _ id ta1 ta2 ta2 (@continuous_id _ (id _))
  have hb := @continuous_inf_dom_right _ _ id tb1 tb2 tb2 (@continuous_id _ (id _))
  have h_continuous_id := @Continuous.prod_map _ _ _ _ ta2 tb2 (ta1 âŠ“ ta2) (tb1 âŠ“ tb2) _ _ ha hb
  exact @Continuous.comp _ _ _ (id _) (id _) _ _ _ h h_continuous_id

/-- A version of `continuous_sInf_dom` for binary functions -/
theorem continuous_sInf_domâ‚‚ {X Y Z} {f : X â†’ Y â†’ Z} {tas : Set (TopologicalSpace X)}
    {tbs : Set (TopologicalSpace Y)} {tX : TopologicalSpace X} {tY : TopologicalSpace Y}
    {tc : TopologicalSpace Z} (hX : tX âˆˆ tas) (hY : tY âˆˆ tbs)
    (hf : Continuous fun p : X Ã— Y => f p.1 p.2) : by
    haveI := sInf tas; haveI := sInf tbs
    exact @Continuous _ _ _ tc fun p : X Ã— Y => f p.1 p.2 := by
  have hX := continuous_sInf_dom hX continuous_id
  have hY := continuous_sInf_dom hY continuous_id
  have h_continuous_id := @Continuous.prod_map _ _ _ _ tX tY (sInf tas) (sInf tbs) _ _ hX hY
  exact @Continuous.comp _ _ _ (id _) (id _) _ _ _ hf h_continuous_id

theorem Filter.Eventually.prod_inl_nhds {p : X â†’ Prop} {x : X} (h : âˆ€á¶  x in ğ“ x, p x) (y : Y) :
    âˆ€á¶  x in ğ“ (x, y), p (x : X Ã— Y).1 :=
  continuousAt_fst h

theorem Filter.Eventually.prod_inr_nhds {p : Y â†’ Prop} {y : Y} (h : âˆ€á¶  x in ğ“ y, p x) (x : X) :
    âˆ€á¶  x in ğ“ (x, y), p (x : X Ã— Y).2 :=
  continuousAt_snd h

theorem Filter.Eventually.prod_mk_nhds {px : X â†’ Prop} {x} (hx : âˆ€á¶  x in ğ“ x, px x) {py : Y â†’ Prop}
    {y} (hy : âˆ€á¶  y in ğ“ y, py y) : âˆ€á¶  p in ğ“ (x, y), px (p : X Ã— Y).1 âˆ§ py p.2 :=
  (hx.prod_inl_nhds y).and (hy.prod_inr_nhds x)

theorem continuous_swap : Continuous (Prod.swap : X Ã— Y â†’ Y Ã— X) :=
  continuous_snd.prod_mk continuous_fst

lemma isClosedMap_swap : IsClosedMap (Prod.swap : X Ã— Y â†’ Y Ã— X) := fun s hs â†¦ by
  rw [image_swap_eq_preimage_swap]
  exact hs.preimage continuous_swap

theorem Continuous.uncurry_left {f : X â†’ Y â†’ Z} (x : X) (h : Continuous (uncurry f)) :
    Continuous (f x) :=
  h.comp (Continuous.Prod.mk _)

theorem Continuous.uncurry_right {f : X â†’ Y â†’ Z} (y : Y) (h : Continuous (uncurry f)) :
    Continuous fun a => f a y :=
  h.comp (Continuous.Prod.mk_left _)

@[deprecated (since := "2024-03-09")] alias continuous_uncurry_left := Continuous.uncurry_left
@[deprecated (since := "2024-03-09")] alias continuous_uncurry_right := Continuous.uncurry_right

theorem continuous_curry {g : X Ã— Y â†’ Z} (x : X) (h : Continuous g) : Continuous (curry g x) :=
  Continuous.uncurry_left x h

theorem IsOpen.prod {s : Set X} {t : Set Y} (hs : IsOpen s) (ht : IsOpen t) : IsOpen (s Ã—Ë¢ t) :=
  (hs.preimage continuous_fst).inter (ht.preimage continuous_snd)

-- Porting note (#11215): TODO: Lean fails to find `tâ‚` and `tâ‚‚` by unification
theorem nhds_prod_eq {x : X} {y : Y} : ğ“ (x, y) = ğ“ x Ã—Ë¢ ğ“ y := by
  dsimp only [SProd.sprod]
  rw [Filter.prod, instTopologicalSpaceProd, nhds_inf (tâ‚ := TopologicalSpace.induced Prod.fst _)
    (tâ‚‚ := TopologicalSpace.induced Prod.snd _), nhds_induced, nhds_induced]

-- Porting note: moved from `Topology.ContinuousOn`
theorem nhdsWithin_prod_eq (x : X) (y : Y) (s : Set X) (t : Set Y) :
    ğ“[s Ã—Ë¢ t] (x, y) = ğ“[s] x Ã—Ë¢ ğ“[t] y := by
  simp only [nhdsWithin, nhds_prod_eq, â† prod_inf_prod, prod_principal_principal]

instance Prod.instNeBotNhdsWithinIio [Preorder X] [Preorder Y] {x : X Ã— Y}
    [hxâ‚ : (ğ“[<] x.1).NeBot] [hxâ‚‚ : (ğ“[<] x.2).NeBot] : (ğ“[<] x).NeBot := by
  refine (hxâ‚.prod hxâ‚‚).mono ?_
  rw [â† nhdsWithin_prod_eq]
  exact nhdsWithin_mono _ fun _ âŸ¨hâ‚, hâ‚‚âŸ© â†¦ Prod.lt_iff.2 <| .inl âŸ¨hâ‚, hâ‚‚.leâŸ©

instance Prod.instNeBotNhdsWithinIoi [Preorder X] [Preorder Y] {x : X Ã— Y}
    [(ğ“[>] x.1).NeBot] [(ğ“[>] x.2).NeBot] : (ğ“[>] x).NeBot :=
  Prod.instNeBotNhdsWithinIio (X := Xáµ’áµˆ) (Y := Yáµ’áµˆ)
    (x := (OrderDual.toDual x.1, OrderDual.toDual x.2))

theorem mem_nhds_prod_iff {x : X} {y : Y} {s : Set (X Ã— Y)} :
    s âˆˆ ğ“ (x, y) â†” âˆƒ u âˆˆ ğ“ x, âˆƒ v âˆˆ ğ“ y, u Ã—Ë¢ v âŠ† s := by rw [nhds_prod_eq, mem_prod_iff]

theorem mem_nhdsWithin_prod_iff {x : X} {y : Y} {s : Set (X Ã— Y)} {tx : Set X} {ty : Set Y} :
    s âˆˆ ğ“[tx Ã—Ë¢ ty] (x, y) â†” âˆƒ u âˆˆ ğ“[tx] x, âˆƒ v âˆˆ ğ“[ty] y, u Ã—Ë¢ v âŠ† s := by
  rw [nhdsWithin_prod_eq, mem_prod_iff]

-- Porting note: moved up
theorem Filter.HasBasis.prod_nhds {Î¹X Î¹Y : Type*} {px : Î¹X â†’ Prop} {py : Î¹Y â†’ Prop}
    {sx : Î¹X â†’ Set X} {sy : Î¹Y â†’ Set Y} {x : X} {y : Y} (hx : (ğ“ x).HasBasis px sx)
    (hy : (ğ“ y).HasBasis py sy) :
    (ğ“ (x, y)).HasBasis (fun i : Î¹X Ã— Î¹Y => px i.1 âˆ§ py i.2) fun i => sx i.1 Ã—Ë¢ sy i.2 := by
  rw [nhds_prod_eq]
  exact hx.prod hy

-- Porting note: moved up
theorem Filter.HasBasis.prod_nhds' {Î¹X Î¹Y : Type*} {pX : Î¹X â†’ Prop} {pY : Î¹Y â†’ Prop}
    {sx : Î¹X â†’ Set X} {sy : Î¹Y â†’ Set Y} {p : X Ã— Y} (hx : (ğ“ p.1).HasBasis pX sx)
    (hy : (ğ“ p.2).HasBasis pY sy) :
    (ğ“ p).HasBasis (fun i : Î¹X Ã— Î¹Y => pX i.1 âˆ§ pY i.2) fun i => sx i.1 Ã—Ë¢ sy i.2 :=
  hx.prod_nhds hy

theorem MapClusterPt.curry_prodMap {Î± Î² : Type*}
    {f : Î± â†’ X} {g : Î² â†’ Y} {la : Filter Î±} {lb : Filter Î²} {x : X} {y : Y}
    (hf : MapClusterPt x la f) (hg : MapClusterPt y lb g) :
    MapClusterPt (x, y) (la.curry lb) (.map f g) := by
  rw [mapClusterPt_iff] at hf hg
  rw [((ğ“ x).basis_sets.prod_nhds (ğ“ y).basis_sets).mapClusterPt_iff_frequently]
  rintro âŸ¨s, tâŸ© âŸ¨hs, htâŸ©
  rw [frequently_curry_iff]
  exact (hf s hs).mono fun x hx â†¦ (hg t ht).mono fun y hy â†¦ âŸ¨hx, hyâŸ©

theorem MapClusterPt.prodMap {Î± Î² : Type*}
    {f : Î± â†’ X} {g : Î² â†’ Y} {la : Filter Î±} {lb : Filter Î²} {x : X} {y : Y}
    (hf : MapClusterPt x la f) (hg : MapClusterPt y lb g) :
    MapClusterPt (x, y) (la Ã—Ë¢ lb) (.map f g) :=
  (hf.curry_prodMap hg).mono <| map_mono curry_le_prod

theorem mem_nhds_prod_iff' {x : X} {y : Y} {s : Set (X Ã— Y)} :
    s âˆˆ ğ“ (x, y) â†” âˆƒ u v, IsOpen u âˆ§ x âˆˆ u âˆ§ IsOpen v âˆ§ y âˆˆ v âˆ§ u Ã—Ë¢ v âŠ† s :=
  ((nhds_basis_opens x).prod_nhds (nhds_basis_opens y)).mem_iff.trans <| by
    simp only [Prod.exists, and_comm, and_assoc, and_left_comm]

theorem Prod.tendsto_iff {X} (seq : X â†’ Y Ã— Z) {f : Filter X} (p : Y Ã— Z) :
    Tendsto seq f (ğ“ p) â†”
      Tendsto (fun n => (seq n).fst) f (ğ“ p.fst) âˆ§ Tendsto (fun n => (seq n).snd) f (ğ“ p.snd) := by
  rw [nhds_prod_eq, Filter.tendsto_prod_iff']

instance [DiscreteTopology X] [DiscreteTopology Y] : DiscreteTopology (X Ã— Y) :=
  discreteTopology_iff_nhds.2 fun (a, b) => by
    rw [nhds_prod_eq, nhds_discrete X, nhds_discrete Y, prod_pure_pure]

theorem prod_mem_nhds_iff {s : Set X} {t : Set Y} {x : X} {y : Y} :
    s Ã—Ë¢ t âˆˆ ğ“ (x, y) â†” s âˆˆ ğ“ x âˆ§ t âˆˆ ğ“ y := by rw [nhds_prod_eq, prod_mem_prod_iff]

theorem prod_mem_nhds {s : Set X} {t : Set Y} {x : X} {y : Y} (hx : s âˆˆ ğ“ x) (hy : t âˆˆ ğ“ y) :
    s Ã—Ë¢ t âˆˆ ğ“ (x, y) :=
  prod_mem_nhds_iff.2 âŸ¨hx, hyâŸ©

theorem isOpen_setOf_disjoint_nhds_nhds : IsOpen { p : X Ã— X | Disjoint (ğ“ p.1) (ğ“ p.2) } := by
  simp only [isOpen_iff_mem_nhds, Prod.forall, mem_setOf_eq]
  intro x y h
  obtain âŸ¨U, hU, V, hV, hdâŸ© := ((nhds_basis_opens x).disjoint_iff (nhds_basis_opens y)).mp h
  exact mem_nhds_prod_iff'.mpr âŸ¨U, V, hU.2, hU.1, hV.2, hV.1, fun âŸ¨x', y'âŸ© âŸ¨hx', hy'âŸ© =>
    disjoint_of_disjoint_of_mem hd (hU.2.mem_nhds hx') (hV.2.mem_nhds hy')âŸ©

theorem Filter.Eventually.prod_nhds {p : X â†’ Prop} {q : Y â†’ Prop} {x : X} {y : Y}
    (hx : âˆ€á¶  x in ğ“ x, p x) (hy : âˆ€á¶  y in ğ“ y, q y) : âˆ€á¶  z : X Ã— Y in ğ“ (x, y), p z.1 âˆ§ q z.2 :=
  prod_mem_nhds hx hy

theorem nhds_swap (x : X) (y : Y) : ğ“ (x, y) = (ğ“ (y, x)).map Prod.swap := by
  rw [nhds_prod_eq, Filter.prod_comm, nhds_prod_eq]; rfl

theorem Filter.Tendsto.prod_mk_nhds {Î³} {x : X} {y : Y} {f : Filter Î³} {mx : Î³ â†’ X} {my : Î³ â†’ Y}
    (hx : Tendsto mx f (ğ“ x)) (hy : Tendsto my f (ğ“ y)) :
    Tendsto (fun c => (mx c, my c)) f (ğ“ (x, y)) := by
  rw [nhds_prod_eq]; exact Filter.Tendsto.prod_mk hx hy

theorem Filter.Eventually.curry_nhds {p : X Ã— Y â†’ Prop} {x : X} {y : Y}
    (h : âˆ€á¶  x in ğ“ (x, y), p x) : âˆ€á¶  x' in ğ“ x, âˆ€á¶  y' in ğ“ y, p (x', y') := by
  rw [nhds_prod_eq] at h
  exact h.curry

@[fun_prop]
theorem ContinuousAt.prod {f : X â†’ Y} {g : X â†’ Z} {x : X} (hf : ContinuousAt f x)
    (hg : ContinuousAt g x) : ContinuousAt (fun x => (f x, g x)) x :=
  hf.prod_mk_nhds hg

theorem ContinuousAt.prod_map {f : X â†’ Z} {g : Y â†’ W} {p : X Ã— Y} (hf : ContinuousAt f p.fst)
    (hg : ContinuousAt g p.snd) : ContinuousAt (fun p : X Ã— Y => (f p.1, g p.2)) p :=
  hf.fst''.prod hg.snd''

theorem ContinuousAt.prod_map' {f : X â†’ Z} {g : Y â†’ W} {x : X} {y : Y} (hf : ContinuousAt f x)
    (hg : ContinuousAt g y) : ContinuousAt (fun p : X Ã— Y => (f p.1, g p.2)) (x, y) :=
  hf.fst'.prod hg.snd'

theorem ContinuousAt.compâ‚‚ {f : Y Ã— Z â†’ W} {g : X â†’ Y} {h : X â†’ Z} {x : X}
    (hf : ContinuousAt f (g x, h x)) (hg : ContinuousAt g x) (hh : ContinuousAt h x) :
    ContinuousAt (fun x â†¦ f (g x, h x)) x :=
  ContinuousAt.comp hf (hg.prod hh)

theorem ContinuousAt.compâ‚‚_of_eq {f : Y Ã— Z â†’ W} {g : X â†’ Y} {h : X â†’ Z} {x : X} {y : Y Ã— Z}
    (hf : ContinuousAt f y) (hg : ContinuousAt g x) (hh : ContinuousAt h x) (e : (g x, h x) = y) :
    ContinuousAt (fun x â†¦ f (g x, h x)) x := by
  rw [â† e] at hf
  exact hf.compâ‚‚ hg hh

/-- Continuous functions on products are continuous in their first argument -/
theorem Continuous.curry_left {f : X Ã— Y â†’ Z} (hf : Continuous f) {y : Y} :
    Continuous fun x â†¦ f (x, y) :=
  hf.comp (continuous_id.prod_mk continuous_const)
alias Continuous.along_fst := Continuous.curry_left

/-- Continuous functions on products are continuous in their second argument -/
theorem Continuous.curry_right {f : X Ã— Y â†’ Z} (hf : Continuous f) {x : X} :
    Continuous fun y â†¦ f (x, y) :=
  hf.comp (continuous_const.prod_mk continuous_id)
alias Continuous.along_snd := Continuous.curry_right

-- todo: prove a version of `generateFrom_union` with `image2 (âˆ©) s t` in the LHS and use it here
theorem prod_generateFrom_generateFrom_eq {X Y : Type*} {s : Set (Set X)} {t : Set (Set Y)}
    (hs : â‹ƒâ‚€ s = univ) (ht : â‹ƒâ‚€ t = univ) :
    @instTopologicalSpaceProd X Y (generateFrom s) (generateFrom t) =
      generateFrom (image2 (Â·  Ã—Ë¢ Â·) s t) :=
  let G := generateFrom (image2  (Â·  Ã—Ë¢ Â·) s t)
  le_antisymm
    (le_generateFrom fun g âŸ¨u, hu, v, hv, g_eqâŸ© =>
      g_eq.symm â–¸
        @IsOpen.prod _ _ (generateFrom s) (generateFrom t) _ _ (GenerateOpen.basic _ hu)
          (GenerateOpen.basic _ hv))
    (le_inf
      (coinduced_le_iff_le_induced.mp <|
        le_generateFrom fun u hu =>
          have : â‹ƒ v âˆˆ t, u Ã—Ë¢ v = Prod.fst â»Â¹' u := by
            simp_rw [â† prod_iUnion, â† sUnion_eq_biUnion, ht, prod_univ]
          show G.IsOpen (Prod.fst â»Â¹' u) by
            rw [â† this]
            exact
              isOpen_iUnion fun v =>
                isOpen_iUnion fun hv => GenerateOpen.basic _ âŸ¨_, hu, _, hv, rflâŸ©)
      (coinduced_le_iff_le_induced.mp <|
        le_generateFrom fun v hv =>
          have : â‹ƒ u âˆˆ s, u Ã—Ë¢ v = Prod.snd â»Â¹' v := by
            simp_rw [â† iUnion_prod_const, â† sUnion_eq_biUnion, hs, univ_prod]
          show G.IsOpen (Prod.snd â»Â¹' v) by
            rw [â† this]
            exact
              isOpen_iUnion fun u =>
                isOpen_iUnion fun hu => GenerateOpen.basic _ âŸ¨_, hu, _, hv, rflâŸ©))

-- todo: use the previous lemma?
theorem prod_eq_generateFrom :
    instTopologicalSpaceProd =
      generateFrom { g | âˆƒ (s : Set X) (t : Set Y), IsOpen s âˆ§ IsOpen t âˆ§ g = s Ã—Ë¢ t } :=
  le_antisymm (le_generateFrom fun g âŸ¨s, t, hs, ht, g_eqâŸ© => g_eq.symm â–¸ hs.prod ht)
    (le_inf
      (forall_mem_image.2 fun t ht =>
        GenerateOpen.basic _ âŸ¨t, univ, by simpa [Set.prod_eq] using htâŸ©)
      (forall_mem_image.2 fun t ht =>
        GenerateOpen.basic _ âŸ¨univ, t, by simpa [Set.prod_eq] using htâŸ©))

-- Porting note (#11215): TODO: align with `mem_nhds_prod_iff'`
theorem isOpen_prod_iff {s : Set (X Ã— Y)} :
    IsOpen s â†” âˆ€ a b, (a, b) âˆˆ s â†’
      âˆƒ u v, IsOpen u âˆ§ IsOpen v âˆ§ a âˆˆ u âˆ§ b âˆˆ v âˆ§ u Ã—Ë¢ v âŠ† s :=
  isOpen_iff_mem_nhds.trans <| by simp_rw [Prod.forall, mem_nhds_prod_iff', and_left_comm]

/-- A product of induced topologies is induced by the product map -/
theorem prod_induced_induced {X Z} (f : X â†’ Y) (g : Z â†’ W) :
    @instTopologicalSpaceProd X Z (induced f â€¹_â€º) (induced g â€¹_â€º) =
      induced (fun p => (f p.1, g p.2)) instTopologicalSpaceProd := by
  delta instTopologicalSpaceProd
  simp_rw [induced_inf, induced_compose]
  rfl

/-- Given a neighborhood `s` of `(x, x)`, then `(x, x)` has a square open neighborhood
  that is a subset of `s`. -/
theorem exists_nhds_square {s : Set (X Ã— X)} {x : X} (hx : s âˆˆ ğ“ (x, x)) :
    âˆƒ U : Set X, IsOpen U âˆ§ x âˆˆ U âˆ§ U Ã—Ë¢ U âŠ† s := by
  simpa [nhds_prod_eq, (nhds_basis_opens x).prod_self.mem_iff, and_assoc, and_left_comm] using hx

/-- `Prod.fst` maps neighborhood of `x : X Ã— Y` within the section `Prod.snd â»Â¹' {x.2}`
to `ğ“ x.1`. -/
theorem map_fst_nhdsWithin (x : X Ã— Y) : map Prod.fst (ğ“[Prod.snd â»Â¹' {x.2}] x) = ğ“ x.1 := by
  refine le_antisymm (continuousAt_fst.mono_left inf_le_left) fun s hs => ?_
  rcases x with âŸ¨x, yâŸ©
  rw [mem_map, nhdsWithin, mem_inf_principal, mem_nhds_prod_iff] at hs
  rcases hs with âŸ¨u, hu, v, hv, HâŸ©
  simp only [prod_subset_iff, mem_singleton_iff, mem_setOf_eq, mem_preimage] at H
  exact mem_of_superset hu fun z hz => H _ hz _ (mem_of_mem_nhds hv) rfl

@[simp]
theorem map_fst_nhds (x : X Ã— Y) : map Prod.fst (ğ“ x) = ğ“ x.1 :=
  le_antisymm continuousAt_fst <| (map_fst_nhdsWithin x).symm.trans_le (map_mono inf_le_left)

/-- The first projection in a product of topological spaces sends open sets to open sets. -/
theorem isOpenMap_fst : IsOpenMap (@Prod.fst X Y) :=
  isOpenMap_iff_nhds_le.2 fun x => (map_fst_nhds x).ge

/-- `Prod.snd` maps neighborhood of `x : X Ã— Y` within the section `Prod.fst â»Â¹' {x.1}`
to `ğ“ x.2`. -/
theorem map_snd_nhdsWithin (x : X Ã— Y) : map Prod.snd (ğ“[Prod.fst â»Â¹' {x.1}] x) = ğ“ x.2 := by
  refine le_antisymm (continuousAt_snd.mono_left inf_le_left) fun s hs => ?_
  rcases x with âŸ¨x, yâŸ©
  rw [mem_map, nhdsWithin, mem_inf_principal, mem_nhds_prod_iff] at hs
  rcases hs with âŸ¨u, hu, v, hv, HâŸ©
  simp only [prod_subset_iff, mem_singleton_iff, mem_setOf_eq, mem_preimage] at H
  exact mem_of_superset hv fun z hz => H _ (mem_of_mem_nhds hu) _ hz rfl

@[simp]
theorem map_snd_nhds (x : X Ã— Y) : map Prod.snd (ğ“ x) = ğ“ x.2 :=
  le_antisymm continuousAt_snd <| (map_snd_nhdsWithin x).symm.trans_le (map_mono inf_le_left)

/-- The second projection in a product of topological spaces sends open sets to open sets. -/
theorem isOpenMap_snd : IsOpenMap (@Prod.snd X Y) :=
  isOpenMap_iff_nhds_le.2 fun x => (map_snd_nhds x).ge

/-- A product set is open in a product space if and only if each factor is open, or one of them is
empty -/
theorem isOpen_prod_iff' {s : Set X} {t : Set Y} :
    IsOpen (s Ã—Ë¢ t) â†” IsOpen s âˆ§ IsOpen t âˆ¨ s = âˆ… âˆ¨ t = âˆ… := by
  rcases (s Ã—Ë¢ t).eq_empty_or_nonempty with h | h
  Â· simp [h, prod_eq_empty_iff.1 h]
  Â· have st : s.Nonempty âˆ§ t.Nonempty := prod_nonempty_iff.1 h
    constructor
    Â· intro (H : IsOpen (s Ã—Ë¢ t))
      refine Or.inl âŸ¨?_, ?_âŸ©
      Â· show IsOpen s
        rw [â† fst_image_prod s st.2]
        exact isOpenMap_fst _ H
      Â· show IsOpen t
        rw [â† snd_image_prod st.1 t]
        exact isOpenMap_snd _ H
    Â· intro H
      simp only [st.1.ne_empty, st.2.ne_empty, not_false_iff, or_false_iff] at H
      exact H.1.prod H.2

theorem quotientMap_fst [Nonempty Y] : QuotientMap (Prod.fst : X Ã— Y â†’ X) :=
  isOpenMap_fst.to_quotientMap continuous_fst Prod.fst_surjective

theorem quotientMap_snd [Nonempty X] : QuotientMap (Prod.snd : X Ã— Y â†’ Y) :=
  isOpenMap_snd.to_quotientMap continuous_snd Prod.snd_surjective

theorem closure_prod_eq {s : Set X} {t : Set Y} : closure (s Ã—Ë¢ t) = closure s Ã—Ë¢ closure t :=
  ext fun âŸ¨a, bâŸ© => by
    simp_rw [mem_prod, mem_closure_iff_nhdsWithin_neBot, nhdsWithin_prod_eq, prod_neBot]

theorem interior_prod_eq (s : Set X) (t : Set Y) : interior (s Ã—Ë¢ t) = interior s Ã—Ë¢ interior t :=
  ext fun âŸ¨a, bâŸ© => by simp only [mem_interior_iff_mem_nhds, mem_prod, prod_mem_nhds_iff]

theorem frontier_prod_eq (s : Set X) (t : Set Y) :
    frontier (s Ã—Ë¢ t) = closure s Ã—Ë¢ frontier t âˆª frontier s Ã—Ë¢ closure t := by
  simp only [frontier, closure_prod_eq, interior_prod_eq, prod_diff_prod]

@[simp]
theorem frontier_prod_univ_eq (s : Set X) :
    frontier (s Ã—Ë¢ (univ : Set Y)) = frontier s Ã—Ë¢ univ := by
  simp [frontier_prod_eq]

@[simp]
theorem frontier_univ_prod_eq (s : Set Y) :
    frontier ((univ : Set X) Ã—Ë¢ s) = univ Ã—Ë¢ frontier s := by
  simp [frontier_prod_eq]

theorem map_mem_closureâ‚‚ {f : X â†’ Y â†’ Z} {x : X} {y : Y} {s : Set X} {t : Set Y} {u : Set Z}
    (hf : Continuous (uncurry f)) (hx : x âˆˆ closure s) (hy : y âˆˆ closure t)
    (h : âˆ€ a âˆˆ s, âˆ€ b âˆˆ t, f a b âˆˆ u) : f x y âˆˆ closure u :=
  have Hâ‚ : (x, y) âˆˆ closure (s Ã—Ë¢ t) := by simpa only [closure_prod_eq] using mk_mem_prod hx hy
  have Hâ‚‚ : MapsTo (uncurry f) (s Ã—Ë¢ t) u := forall_prod_set.2 h
  Hâ‚‚.closure hf Hâ‚

theorem IsClosed.prod {sâ‚ : Set X} {sâ‚‚ : Set Y} (hâ‚ : IsClosed sâ‚) (hâ‚‚ : IsClosed sâ‚‚) :
    IsClosed (sâ‚ Ã—Ë¢ sâ‚‚) :=
  closure_eq_iff_isClosed.mp <| by simp only [hâ‚.closure_eq, hâ‚‚.closure_eq, closure_prod_eq]

/-- The product of two dense sets is a dense set. -/
theorem Dense.prod {s : Set X} {t : Set Y} (hs : Dense s) (ht : Dense t) : Dense (s Ã—Ë¢ t) :=
  fun x => by
  rw [closure_prod_eq]
  exact âŸ¨hs x.1, ht x.2âŸ©

/-- If `f` and `g` are maps with dense range, then `Prod.map f g` has dense range. -/
theorem DenseRange.prod_map {Î¹ : Type*} {Îº : Type*} {f : Î¹ â†’ Y} {g : Îº â†’ Z} (hf : DenseRange f)
    (hg : DenseRange g) : DenseRange (Prod.map f g) := by
  simpa only [DenseRange, prod_range_range_eq] using hf.prod hg

theorem Inducing.prod_map {f : X â†’ Y} {g : Z â†’ W} (hf : Inducing f) (hg : Inducing g) :
    Inducing (Prod.map f g) :=
  inducing_iff_nhds.2 fun (x, z) => by simp_rw [Prod.map_def, nhds_prod_eq, hf.nhds_eq_comap,
    hg.nhds_eq_comap, prod_comap_comap_eq]

@[simp]
theorem inducing_const_prod {x : X} {f : Y â†’ Z} : (Inducing fun x' => (x, f x')) â†” Inducing f := by
  simp_rw [inducing_iff, instTopologicalSpaceProd, induced_inf, induced_compose, Function.comp_def,
    induced_const, top_inf_eq]

@[simp]
theorem inducing_prod_const {y : Y} {f : X â†’ Z} : (Inducing fun x => (f x, y)) â†” Inducing f := by
  simp_rw [inducing_iff, instTopologicalSpaceProd, induced_inf, induced_compose, Function.comp_def,
    induced_const, inf_top_eq]

theorem Embedding.prod_map {f : X â†’ Y} {g : Z â†’ W} (hf : Embedding f) (hg : Embedding g) :
    Embedding (Prod.map f g) :=
  { hf.toInducing.prod_map hg.toInducing with
    inj := fun âŸ¨xâ‚, zâ‚âŸ© âŸ¨xâ‚‚, zâ‚‚âŸ© => by simp [hf.inj.eq_iff, hg.inj.eq_iff] }

protected theorem IsOpenMap.prod {f : X â†’ Y} {g : Z â†’ W} (hf : IsOpenMap f) (hg : IsOpenMap g) :
    IsOpenMap fun p : X Ã— Z => (f p.1, g p.2) := by
  rw [isOpenMap_iff_nhds_le]
  rintro âŸ¨a, bâŸ©
  rw [nhds_prod_eq, nhds_prod_eq, â† Filter.prod_map_map_eq]
  exact Filter.prod_mono (hf.nhds_le a) (hg.nhds_le b)

protected theorem OpenEmbedding.prod {f : X â†’ Y} {g : Z â†’ W} (hf : OpenEmbedding f)
    (hg : OpenEmbedding g) : OpenEmbedding fun x : X Ã— Z => (f x.1, g x.2) :=
  openEmbedding_of_embedding_open (hf.1.prod_map hg.1) (hf.isOpenMap.prod hg.isOpenMap)

theorem embedding_graph {f : X â†’ Y} (hf : Continuous f) : Embedding fun x => (x, f x) :=
  embedding_of_embedding_compose (continuous_id.prod_mk hf) continuous_fst embedding_id

theorem embedding_prod_mk (x : X) : Embedding (Prod.mk x : Y â†’ X Ã— Y) :=
  embedding_of_embedding_compose (Continuous.Prod.mk x) continuous_snd embedding_id

end Prod

section Bool

lemma continuous_bool_rng [TopologicalSpace X] {f : X â†’ Bool} (b : Bool) :
    Continuous f â†” IsClopen (f â»Â¹' {b}) := by
  rw [continuous_discrete_rng, Bool.forall_bool' b, IsClopen, â† isOpen_compl_iff, â† preimage_compl,
    Bool.compl_singleton, and_comm]

end Bool

section Sum

open Sum

variable [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z] [TopologicalSpace W]

theorem continuous_sum_dom {f : X âŠ• Y â†’ Z} :
    Continuous f â†” Continuous (f âˆ˜ Sum.inl) âˆ§ Continuous (f âˆ˜ Sum.inr) :=
  (continuous_sup_dom (tâ‚ := TopologicalSpace.coinduced Sum.inl _)
    (tâ‚‚ := TopologicalSpace.coinduced Sum.inr _)).trans <|
    continuous_coinduced_dom.and continuous_coinduced_dom

theorem continuous_sum_elim {f : X â†’ Z} {g : Y â†’ Z} :
    Continuous (Sum.elim f g) â†” Continuous f âˆ§ Continuous g :=
  continuous_sum_dom

@[continuity, fun_prop]
theorem Continuous.sum_elim {f : X â†’ Z} {g : Y â†’ Z} (hf : Continuous f) (hg : Continuous g) :
    Continuous (Sum.elim f g) :=
  continuous_sum_elim.2 âŸ¨hf, hgâŸ©

@[continuity, fun_prop]
theorem continuous_isLeft : Continuous (isLeft : X âŠ• Y â†’ Bool) :=
  continuous_sum_dom.2 âŸ¨continuous_const, continuous_constâŸ©

@[continuity, fun_prop]
theorem continuous_isRight : Continuous (isRight : X âŠ• Y â†’ Bool) :=
  continuous_sum_dom.2 âŸ¨continuous_const, continuous_constâŸ©

@[continuity, fun_prop]
-- Porting note: the proof was `continuous_sup_rng_left continuous_coinduced_rng`
theorem continuous_inl : Continuous (@inl X Y) := âŸ¨fun _ => And.leftâŸ©

@[continuity, fun_prop]
-- Porting note: the proof was `continuous_sup_rng_right continuous_coinduced_rng`
theorem continuous_inr : Continuous (@inr X Y) := âŸ¨fun _ => And.rightâŸ©

@[fun_prop, continuity]
lemma continuous_sum_swap : Continuous (@Sum.swap X Y) :=
  Continuous.sum_elim continuous_inr continuous_inl

theorem isOpen_sum_iff {s : Set (X âŠ• Y)} : IsOpen s â†” IsOpen (inl â»Â¹' s) âˆ§ IsOpen (inr â»Â¹' s) :=
  Iff.rfl

theorem isClosed_sum_iff {s : Set (X âŠ• Y)} :
    IsClosed s â†” IsClosed (inl â»Â¹' s) âˆ§ IsClosed (inr â»Â¹' s) := by
  simp only [â† isOpen_compl_iff, isOpen_sum_iff, preimage_compl]

theorem isOpenMap_inl : IsOpenMap (@inl X Y) := fun u hu => by
  simpa [isOpen_sum_iff, preimage_image_eq u Sum.inl_injective]

theorem isOpenMap_inr : IsOpenMap (@inr X Y) := fun u hu => by
  simpa [isOpen_sum_iff, preimage_image_eq u Sum.inr_injective]

theorem openEmbedding_inl : OpenEmbedding (@inl X Y) :=
  openEmbedding_of_continuous_injective_open continuous_inl inl_injective isOpenMap_inl

theorem openEmbedding_inr : OpenEmbedding (@inr X Y) :=
  openEmbedding_of_continuous_injective_open continuous_inr inr_injective isOpenMap_inr

theorem embedding_inl : Embedding (@inl X Y) :=
  openEmbedding_inl.1

theorem embedding_inr : Embedding (@inr X Y) :=
  openEmbedding_inr.1

theorem isOpen_range_inl : IsOpen (range (inl : X â†’ X âŠ• Y)) :=
  openEmbedding_inl.2

theorem isOpen_range_inr : IsOpen (range (inr : Y â†’ X âŠ• Y)) :=
  openEmbedding_inr.2

theorem isClosed_range_inl : IsClosed (range (inl : X â†’ X âŠ• Y)) := by
  rw [â† isOpen_compl_iff, compl_range_inl]
  exact isOpen_range_inr

theorem isClosed_range_inr : IsClosed (range (inr : Y â†’ X âŠ• Y)) := by
  rw [â† isOpen_compl_iff, compl_range_inr]
  exact isOpen_range_inl

theorem closedEmbedding_inl : ClosedEmbedding (inl : X â†’ X âŠ• Y) :=
  âŸ¨embedding_inl, isClosed_range_inlâŸ©

theorem closedEmbedding_inr : ClosedEmbedding (inr : Y â†’ X âŠ• Y) :=
  âŸ¨embedding_inr, isClosed_range_inrâŸ©

theorem nhds_inl (x : X) : ğ“ (inl x : X âŠ• Y) = map inl (ğ“ x) :=
  (openEmbedding_inl.map_nhds_eq _).symm

theorem nhds_inr (y : Y) : ğ“ (inr y : X âŠ• Y) = map inr (ğ“ y) :=
  (openEmbedding_inr.map_nhds_eq _).symm

@[simp]
theorem continuous_sum_map {f : X â†’ Y} {g : Z â†’ W} :
    Continuous (Sum.map f g) â†” Continuous f âˆ§ Continuous g :=
  continuous_sum_elim.trans <|
    embedding_inl.continuous_iff.symm.and embedding_inr.continuous_iff.symm

@[continuity, fun_prop]
theorem Continuous.sum_map {f : X â†’ Y} {g : Z â†’ W} (hf : Continuous f) (hg : Continuous g) :
    Continuous (Sum.map f g) :=
  continuous_sum_map.2 âŸ¨hf, hgâŸ©

theorem isOpenMap_sum {f : X âŠ• Y â†’ Z} :
    IsOpenMap f â†” (IsOpenMap fun a => f (inl a)) âˆ§ IsOpenMap fun b => f (inr b) := by
  simp only [isOpenMap_iff_nhds_le, Sum.forall, nhds_inl, nhds_inr, Filter.map_map, comp_def]

theorem IsOpenMap.sumMap {f : X â†’ Y} {g : Z â†’ W} (hf : IsOpenMap f) (hg : IsOpenMap g) :
    IsOpenMap (Sum.map f g) := by
  exact isOpenMap_sum.2 âŸ¨isOpenMap_inl.comp hf,isOpenMap_inr.comp hgâŸ©

@[simp]
theorem isOpenMap_sum_elim {f : X â†’ Z} {g : Y â†’ Z} :
    IsOpenMap (Sum.elim f g) â†” IsOpenMap f âˆ§ IsOpenMap g := by
  simp only [isOpenMap_sum, elim_inl, elim_inr]

theorem IsOpenMap.sum_elim {f : X â†’ Z} {g : Y â†’ Z} (hf : IsOpenMap f) (hg : IsOpenMap g) :
    IsOpenMap (Sum.elim f g) :=
  isOpenMap_sum_elim.2 âŸ¨hf, hgâŸ©

theorem isClosedMap_sum {f : X âŠ• Y â†’ Z} :
    IsClosedMap f â†” (IsClosedMap fun a => f (.inl a)) âˆ§ IsClosedMap fun b => f (.inr b) := by
  constructor
  Â· intro h
    exact âŸ¨h.comp closedEmbedding_inl.isClosedMap, h.comp closedEmbedding_inr.isClosedMapâŸ©
  Â· rintro h Z hZ
    rw [isClosed_sum_iff] at hZ
    convert (h.1 _ hZ.1).union (h.2 _ hZ.2)
    ext
    simp only [mem_image, Sum.exists, mem_union, mem_preimage]

end Sum

section Subtype

variable [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z] {p : X â†’ Prop}

theorem inducing_subtype_val {t : Set Y} : Inducing ((â†‘) : t â†’ Y) := âŸ¨rflâŸ©

theorem Inducing.of_codRestrict {f : X â†’ Y} {t : Set Y} (ht : âˆ€ x, f x âˆˆ t)
    (h : Inducing (t.codRestrict f ht)) : Inducing f :=
  inducing_subtype_val.comp h

theorem embedding_subtype_val : Embedding ((â†‘) : Subtype p â†’ X) :=
  âŸ¨inducing_subtype_val, Subtype.coe_injectiveâŸ©

theorem closedEmbedding_subtype_val (h : IsClosed { a | p a }) :
    ClosedEmbedding ((â†‘) : Subtype p â†’ X) :=
  âŸ¨embedding_subtype_val, by rwa [Subtype.range_coe_subtype]âŸ©

@[continuity, fun_prop]
theorem continuous_subtype_val : Continuous (@Subtype.val X p) :=
  continuous_induced_dom

theorem Continuous.subtype_val {f : Y â†’ Subtype p} (hf : Continuous f) :
    Continuous fun x => (f x : X) :=
  continuous_subtype_val.comp hf

theorem IsOpen.openEmbedding_subtype_val {s : Set X} (hs : IsOpen s) :
    OpenEmbedding ((â†‘) : s â†’ X) :=
  âŸ¨embedding_subtype_val, (@Subtype.range_coe _ s).symm â–¸ hsâŸ©

theorem IsOpen.isOpenMap_subtype_val {s : Set X} (hs : IsOpen s) : IsOpenMap ((â†‘) : s â†’ X) :=
  hs.openEmbedding_subtype_val.isOpenMap

theorem IsOpenMap.restrict {f : X â†’ Y} (hf : IsOpenMap f) {s : Set X} (hs : IsOpen s) :
    IsOpenMap (s.restrict f) :=
  hf.comp hs.isOpenMap_subtype_val

nonrec theorem IsClosed.closedEmbedding_subtype_val {s : Set X} (hs : IsClosed s) :
    ClosedEmbedding ((â†‘) : s â†’ X) :=
  closedEmbedding_subtype_val hs

theorem IsClosed.isClosedMap_subtype_val {s : Set X} (hs : IsClosed s) :
    IsClosedMap ((â†‘) : s â†’ X) :=
  hs.closedEmbedding_subtype_val.isClosedMap

@[continuity, fun_prop]
theorem Continuous.subtype_mk {f : Y â†’ X} (h : Continuous f) (hp : âˆ€ x, p (f x)) :
    Continuous fun x => (âŸ¨f x, hp xâŸ© : Subtype p) :=
  continuous_induced_rng.2 h

theorem Continuous.subtype_map {f : X â†’ Y} (h : Continuous f) {q : Y â†’ Prop}
    (hpq : âˆ€ x, p x â†’ q (f x)) : Continuous (Subtype.map f hpq) :=
  (h.comp continuous_subtype_val).subtype_mk _

theorem continuous_inclusion {s t : Set X} (h : s âŠ† t) : Continuous (inclusion h) :=
  continuous_id.subtype_map h

theorem continuousAt_subtype_val {p : X â†’ Prop} {x : Subtype p} :
    ContinuousAt ((â†‘) : Subtype p â†’ X) x :=
  continuous_subtype_val.continuousAt

theorem Subtype.dense_iff {s : Set X} {t : Set s} : Dense t â†” s âŠ† closure ((â†‘) '' t) := by
  rw [inducing_subtype_val.dense_iff, SetCoe.forall]
  rfl

theorem map_nhds_subtype_val {s : Set X} (x : s) : map ((â†‘) : s â†’ X) (ğ“ x) = ğ“[s] â†‘x := by
  rw [inducing_subtype_val.map_nhds_eq, Subtype.range_val]

theorem map_nhds_subtype_coe_eq_nhds {x : X} (hx : p x) (h : âˆ€á¶  x in ğ“ x, p x) :
    map ((â†‘) : Subtype p â†’ X) (ğ“ âŸ¨x, hxâŸ©) = ğ“ x :=
  map_nhds_induced_of_mem <| by rw [Subtype.range_val]; exact h

theorem nhds_subtype_eq_comap {x : X} {h : p x} : ğ“ (âŸ¨x, hâŸ© : Subtype p) = comap (â†‘) (ğ“ x) :=
  nhds_induced _ _

theorem tendsto_subtype_rng {Y : Type*} {p : X â†’ Prop} {l : Filter Y} {f : Y â†’ Subtype p} :
    âˆ€ {x : Subtype p}, Tendsto f l (ğ“ x) â†” Tendsto (fun x => (f x : X)) l (ğ“ (x : X))
  | âŸ¨a, haâŸ© => by rw [nhds_subtype_eq_comap, tendsto_comap_iff]; rfl

theorem closure_subtype {x : { a // p a }} {s : Set { a // p a }} :
    x âˆˆ closure s â†” (x : X) âˆˆ closure (((â†‘) : _ â†’ X) '' s) :=
  closure_induced

@[simp]
theorem continuousAt_codRestrict_iff {f : X â†’ Y} {t : Set Y} (h1 : âˆ€ x, f x âˆˆ t) {x : X} :
    ContinuousAt (codRestrict f t h1) x â†” ContinuousAt f x :=
  inducing_subtype_val.continuousAt_iff

alias âŸ¨_, ContinuousAt.codRestrictâŸ© := continuousAt_codRestrict_iff

theorem ContinuousAt.restrict {f : X â†’ Y} {s : Set X} {t : Set Y} (h1 : MapsTo f s t) {x : s}
    (h2 : ContinuousAt f x) : ContinuousAt (h1.restrict f s t) x :=
  (h2.comp continuousAt_subtype_val).codRestrict _

theorem ContinuousAt.restrictPreimage {f : X â†’ Y} {s : Set Y} {x : f â»Â¹' s} (h : ContinuousAt f x) :
    ContinuousAt (s.restrictPreimage f) x :=
  h.restrict _

@[continuity, fun_prop]
theorem Continuous.codRestrict {f : X â†’ Y} {s : Set Y} (hf : Continuous f) (hs : âˆ€ a, f a âˆˆ s) :
    Continuous (s.codRestrict f hs) :=
  hf.subtype_mk hs

@[continuity, fun_prop]
theorem Continuous.restrict {f : X â†’ Y} {s : Set X} {t : Set Y} (h1 : MapsTo f s t)
    (h2 : Continuous f) : Continuous (h1.restrict f s t) :=
  (h2.comp continuous_subtype_val).codRestrict _

@[continuity, fun_prop]
theorem Continuous.restrictPreimage {f : X â†’ Y} {s : Set Y} (h : Continuous f) :
    Continuous (s.restrictPreimage f) :=
  h.restrict _

theorem Inducing.codRestrict {e : X â†’ Y} (he : Inducing e) {s : Set Y} (hs : âˆ€ x, e x âˆˆ s) :
    Inducing (codRestrict e s hs) :=
  inducing_of_inducing_compose (he.continuous.codRestrict hs) continuous_subtype_val he

theorem Embedding.codRestrict {e : X â†’ Y} (he : Embedding e) (s : Set Y) (hs : âˆ€ x, e x âˆˆ s) :
    Embedding (codRestrict e s hs) :=
  embedding_of_embedding_compose (he.continuous.codRestrict hs) continuous_subtype_val he

theorem embedding_inclusion {s t : Set X} (h : s âŠ† t) : Embedding (inclusion h) :=
  embedding_subtype_val.codRestrict _ _

/-- Let `s, t âŠ† X` be two subsets of a topological space `X`.  If `t âŠ† s` and the topology induced
by `X`on `s` is discrete, then also the topology induces on `t` is discrete. -/
theorem DiscreteTopology.of_subset {X : Type*} [TopologicalSpace X] {s t : Set X}
    (_ : DiscreteTopology s) (ts : t âŠ† s) : DiscreteTopology t :=
  (embedding_inclusion ts).discreteTopology

/-- Let `s` be a discrete subset of a topological space. Then the preimage of `s` by
a continuous injective map is also discrete. -/
theorem DiscreteTopology.preimage_of_continuous_injective {X Y : Type*} [TopologicalSpace X]
    [TopologicalSpace Y] (s : Set Y) [DiscreteTopology s] {f : X â†’ Y} (hc : Continuous f)
    (hinj : Function.Injective f) : DiscreteTopology (f â»Â¹' s) :=
  DiscreteTopology.of_continuous_injective (Î² := s) (Continuous.restrict
    (by exact fun _ x â†¦ x) hc) ((MapsTo.restrict_inj _).mpr hinj.injOn)

/-- If `f : X â†’ Y` is a quotient map,
then its restriction to the preimage of an open set is a quotient map too. -/
theorem QuotientMap.restrictPreimage_isOpen {f : X â†’ Y} (hf : QuotientMap f)
    {s : Set Y} (hs : IsOpen s) : QuotientMap (s.restrictPreimage f) := by
  refine quotientMap_iff.2 âŸ¨hf.surjective.restrictPreimage _, fun U â†¦ ?_âŸ©
  rw [hs.openEmbedding_subtype_val.open_iff_image_open, â† hf.isOpen_preimage,
    (hs.preimage hf.continuous).openEmbedding_subtype_val.open_iff_image_open,
    image_val_preimage_restrictPreimage]

open scoped Set.Notation in
lemma isClosed_preimage_val {s t : Set X} : IsClosed (s â†“âˆ© t) â†” s âˆ© closure (s âˆ© t) âŠ† t := by
  rw [â† closure_eq_iff_isClosed, embedding_subtype_val.closure_eq_preimage_closure_image,
    â† Subtype.val_injective.image_injective.eq_iff, Subtype.image_preimage_coe,
    Subtype.image_preimage_coe, subset_antisymm_iff, and_iff_left, Set.subset_inter_iff,
    and_iff_right]
  exacts [Set.inter_subset_left, Set.subset_inter Set.inter_subset_left subset_closure]
end Subtype

section Quotient

variable [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]
variable {r : X â†’ X â†’ Prop} {s : Setoid X}

theorem quotientMap_quot_mk : QuotientMap (@Quot.mk X r) :=
  âŸ¨Quot.exists_rep, rflâŸ©

@[continuity, fun_prop]
theorem continuous_quot_mk : Continuous (@Quot.mk X r) :=
  continuous_coinduced_rng

@[continuity, fun_prop]
theorem continuous_quot_lift {f : X â†’ Y} (hr : âˆ€ a b, r a b â†’ f a = f b) (h : Continuous f) :
    Continuous (Quot.lift f hr : Quot r â†’ Y) :=
  continuous_coinduced_dom.2 h

theorem quotientMap_quotient_mk' : QuotientMap (@Quotient.mk' X s) :=
  quotientMap_quot_mk

theorem continuous_quotient_mk' : Continuous (@Quotient.mk' X s) :=
  continuous_coinduced_rng

theorem Continuous.quotient_lift {f : X â†’ Y} (h : Continuous f) (hs : âˆ€ a b, a â‰ˆ b â†’ f a = f b) :
    Continuous (Quotient.lift f hs : Quotient s â†’ Y) :=
  continuous_coinduced_dom.2 h

theorem Continuous.quotient_liftOn' {f : X â†’ Y} (h : Continuous f)
    (hs : âˆ€ a b, @Setoid.r _ s a b â†’ f a = f b) :
    Continuous (fun x => Quotient.liftOn' x f hs : Quotient s â†’ Y) :=
  h.quotient_lift hs

@[continuity, fun_prop]
theorem Continuous.quotient_map' {t : Setoid Y} {f : X â†’ Y} (hf : Continuous f)
    (H : (s.r â‡’ t.r) f f) : Continuous (Quotient.map' f H) :=
  (continuous_quotient_mk'.comp hf).quotient_lift _

end Quotient

section Pi

variable {Î¹ : Type*} {Ï€ : Î¹ â†’ Type*} {Îº : Type*} [TopologicalSpace X]
  [T : âˆ€ i, TopologicalSpace (Ï€ i)] {f : X â†’ âˆ€ i : Î¹, Ï€ i}

theorem continuous_pi_iff : Continuous f â†” âˆ€ i, Continuous fun a => f a i := by
  simp only [continuous_iInf_rng, continuous_induced_rng, comp_def]

@[continuity, fun_prop]
theorem continuous_pi (h : âˆ€ i, Continuous fun a => f a i) : Continuous f :=
  continuous_pi_iff.2 h

@[continuity, fun_prop]
theorem continuous_apply (i : Î¹) : Continuous fun p : âˆ€ i, Ï€ i => p i :=
  continuous_iInf_dom continuous_induced_dom

@[continuity]
theorem continuous_apply_apply {Ï : Îº â†’ Î¹ â†’ Type*} [âˆ€ j i, TopologicalSpace (Ï j i)] (j : Îº)
    (i : Î¹) : Continuous fun p : âˆ€ j, âˆ€ i, Ï j i => p j i :=
  (continuous_apply i).comp (continuous_apply j)

theorem continuousAt_apply (i : Î¹) (x : âˆ€ i, Ï€ i) : ContinuousAt (fun p : âˆ€ i, Ï€ i => p i) x :=
  (continuous_apply i).continuousAt

theorem Filter.Tendsto.apply_nhds {l : Filter Y} {f : Y â†’ âˆ€ i, Ï€ i} {x : âˆ€ i, Ï€ i}
    (h : Tendsto f l (ğ“ x)) (i : Î¹) : Tendsto (fun a => f a i) l (ğ“ <| x i) :=
  (continuousAt_apply i _).tendsto.comp h

theorem nhds_pi {a : âˆ€ i, Ï€ i} : ğ“ a = pi fun i => ğ“ (a i) := by
  simp only [nhds_iInf, nhds_induced, Filter.pi]

theorem tendsto_pi_nhds {f : Y â†’ âˆ€ i, Ï€ i} {g : âˆ€ i, Ï€ i} {u : Filter Y} :
    Tendsto f u (ğ“ g) â†” âˆ€ x, Tendsto (fun i => f i x) u (ğ“ (g x)) := by
  rw [nhds_pi, Filter.tendsto_pi]

theorem continuousAt_pi {f : X â†’ âˆ€ i, Ï€ i} {x : X} :
    ContinuousAt f x â†” âˆ€ i, ContinuousAt (fun y => f y i) x :=
  tendsto_pi_nhds

@[fun_prop]
theorem continuousAt_pi' {f : X â†’ âˆ€ i, Ï€ i} {x : X} (hf : âˆ€ i, ContinuousAt (fun y => f y i) x) :
    ContinuousAt f x :=
  continuousAt_pi.2 hf

theorem Pi.continuous_precomp' {Î¹' : Type*} (Ï† : Î¹' â†’ Î¹) :
    Continuous (fun (f : (âˆ€ i, Ï€ i)) (j : Î¹') â†¦ f (Ï† j)) :=
  continuous_pi fun j â†¦ continuous_apply (Ï† j)

theorem Pi.continuous_precomp {Î¹' : Type*} (Ï† : Î¹' â†’ Î¹) :
    Continuous (Â· âˆ˜ Ï† : (Î¹ â†’ X) â†’ (Î¹' â†’ X)) :=
  Pi.continuous_precomp' Ï†

theorem Pi.continuous_postcomp' {X : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (X i)]
    {g : âˆ€ i, Ï€ i â†’ X i} (hg : âˆ€ i, Continuous (g i)) :
    Continuous (fun (f : (âˆ€ i, Ï€ i)) (i : Î¹) â†¦ g i (f i)) :=
  continuous_pi fun i â†¦ (hg i).comp <| continuous_apply i

theorem Pi.continuous_postcomp [TopologicalSpace Y] {g : X â†’ Y} (hg : Continuous g) :
    Continuous (g âˆ˜ Â· : (Î¹ â†’ X) â†’ (Î¹ â†’ Y)) :=
  Pi.continuous_postcomp' fun _ â†¦ hg

lemma Pi.induced_precomp' {Î¹' : Type*} (Ï† : Î¹' â†’ Î¹) :
    induced (fun (f : (âˆ€ i, Ï€ i)) (j : Î¹') â†¦ f (Ï† j)) Pi.topologicalSpace =
    â¨… i', induced (eval (Ï† i')) (T (Ï† i')) := by
  simp [Pi.topologicalSpace, induced_iInf, induced_compose, comp_def]

lemma Pi.induced_precomp [TopologicalSpace Y] {Î¹' : Type*} (Ï† : Î¹' â†’ Î¹) :
    induced (Â· âˆ˜ Ï†) Pi.topologicalSpace =
    â¨… i', induced (eval (Ï† i')) â€¹TopologicalSpace Yâ€º :=
  induced_precomp' Ï†

lemma Pi.continuous_restrict (S : Set Î¹) :
    Continuous (S.restrict : (âˆ€ i : Î¹, Ï€ i) â†’ (âˆ€ i : S, Ï€ i)) :=
  Pi.continuous_precomp' ((â†‘) : S â†’ Î¹)

lemma Pi.induced_restrict (S : Set Î¹) :
    induced (S.restrict) Pi.topologicalSpace =
    â¨… i âˆˆ S, induced (eval i) (T i) := by
  simp (config := { unfoldPartialApp := true }) [â† iInf_subtype'', â† induced_precomp' ((â†‘) : S â†’ Î¹),
    restrict]

lemma Pi.induced_restrict_sUnion (ğ”– : Set (Set Î¹)) :
    induced (â‹ƒâ‚€ ğ”–).restrict (Pi.topologicalSpace (Y := fun i : (â‹ƒâ‚€ ğ”–) â†¦ Ï€ i)) =
    â¨… S âˆˆ ğ”–, induced S.restrict Pi.topologicalSpace := by
  simp_rw [Pi.induced_restrict, iInf_sUnion]

theorem Filter.Tendsto.update [DecidableEq Î¹] {l : Filter Y} {f : Y â†’ âˆ€ i, Ï€ i} {x : âˆ€ i, Ï€ i}
    (hf : Tendsto f l (ğ“ x)) (i : Î¹) {g : Y â†’ Ï€ i} {xi : Ï€ i} (hg : Tendsto g l (ğ“ xi)) :
    Tendsto (fun a => update (f a) i (g a)) l (ğ“ <| update x i xi) :=
  tendsto_pi_nhds.2 fun j => by rcases eq_or_ne j i with (rfl | hj) <;> simp [*, hf.apply_nhds]

theorem ContinuousAt.update [DecidableEq Î¹] {x : X} (hf : ContinuousAt f x) (i : Î¹) {g : X â†’ Ï€ i}
    (hg : ContinuousAt g x) : ContinuousAt (fun a => update (f a) i (g a)) x :=
  hf.tendsto.update i hg

theorem Continuous.update [DecidableEq Î¹] (hf : Continuous f) (i : Î¹) {g : X â†’ Ï€ i}
    (hg : Continuous g) : Continuous fun a => update (f a) i (g a) :=
  continuous_iff_continuousAt.2 fun _ => hf.continuousAt.update i hg.continuousAt

/-- `Function.update f i x` is continuous in `(f, x)`. -/
@[continuity, fun_prop]
theorem continuous_update [DecidableEq Î¹] (i : Î¹) :
    Continuous fun f : (âˆ€ j, Ï€ j) Ã— Ï€ i => update f.1 i f.2 :=
  continuous_fst.update i continuous_snd

/-- `Pi.mulSingle i x` is continuous in `x`. -/
-- Porting note (#11215): TODO: restore @[continuity]
@[to_additive "`Pi.single i x` is continuous in `x`."]
theorem continuous_mulSingle [âˆ€ i, One (Ï€ i)] [DecidableEq Î¹] (i : Î¹) :
    Continuous fun x => (Pi.mulSingle i x : âˆ€ i, Ï€ i) :=
  continuous_const.update _ continuous_id

theorem Filter.Tendsto.fin_insertNth {n} {Ï€ : Fin (n + 1) â†’ Type*} [âˆ€ i, TopologicalSpace (Ï€ i)]
    (i : Fin (n + 1)) {f : Y â†’ Ï€ i} {l : Filter Y} {x : Ï€ i} (hf : Tendsto f l (ğ“ x))
    {g : Y â†’ âˆ€ j : Fin n, Ï€ (i.succAbove j)} {y : âˆ€ j, Ï€ (i.succAbove j)} (hg : Tendsto g l (ğ“ y)) :
    Tendsto (fun a => i.insertNth (f a) (g a)) l (ğ“ <| i.insertNth x y) :=
  tendsto_pi_nhds.2 fun j => Fin.succAboveCases i (by simpa) (by simpa using tendsto_pi_nhds.1 hg) j

theorem ContinuousAt.fin_insertNth {n} {Ï€ : Fin (n + 1) â†’ Type*} [âˆ€ i, TopologicalSpace (Ï€ i)]
    (i : Fin (n + 1)) {f : X â†’ Ï€ i} {x : X} (hf : ContinuousAt f x)
    {g : X â†’ âˆ€ j : Fin n, Ï€ (i.succAbove j)} (hg : ContinuousAt g x) :
    ContinuousAt (fun a => i.insertNth (f a) (g a)) x :=
  hf.tendsto.fin_insertNth i hg

theorem Continuous.fin_insertNth {n} {Ï€ : Fin (n + 1) â†’ Type*} [âˆ€ i, TopologicalSpace (Ï€ i)]
    (i : Fin (n + 1)) {f : X â†’ Ï€ i} (hf : Continuous f) {g : X â†’ âˆ€ j : Fin n, Ï€ (i.succAbove j)}
    (hg : Continuous g) : Continuous fun a => i.insertNth (f a) (g a) :=
  continuous_iff_continuousAt.2 fun _ => hf.continuousAt.fin_insertNth i hg.continuousAt

theorem isOpen_set_pi {i : Set Î¹} {s : âˆ€ a, Set (Ï€ a)} (hi : i.Finite)
    (hs : âˆ€ a âˆˆ i, IsOpen (s a)) : IsOpen (pi i s) := by
  rw [pi_def]; exact hi.isOpen_biInter fun a ha => (hs _ ha).preimage (continuous_apply _)

theorem isOpen_pi_iff {s : Set (âˆ€ a, Ï€ a)} :
    IsOpen s â†”
      âˆ€ f, f âˆˆ s â†’ âˆƒ (I : Finset Î¹) (u : âˆ€ a, Set (Ï€ a)),
        (âˆ€ a, a âˆˆ I â†’ IsOpen (u a) âˆ§ f a âˆˆ u a) âˆ§ (I : Set Î¹).pi u âŠ† s := by
  rw [isOpen_iff_nhds]
  simp_rw [le_principal_iff, nhds_pi, Filter.mem_pi', mem_nhds_iff]
  refine forallâ‚‚_congr fun a _ => âŸ¨?_, ?_âŸ©
  Â· rintro âŸ¨I, t, âŸ¨h1, h2âŸ©âŸ©
    refine âŸ¨I, fun a => eval a '' (I : Set Î¹).pi fun a => (h1 a).choose, fun i hi => ?_, ?_âŸ©
    Â· simp_rw [eval_image_pi (Finset.mem_coe.mpr hi)
          (pi_nonempty_iff.mpr fun i => âŸ¨_, fun _ => (h1 i).choose_spec.2.2âŸ©)]
      exact (h1 i).choose_spec.2
    Â· exact Subset.trans
        (pi_mono fun i hi => (eval_image_pi_subset hi).trans (h1 i).choose_spec.1) h2
  Â· rintro âŸ¨I, t, âŸ¨h1, h2âŸ©âŸ©
    classical
    refine âŸ¨I, fun a => ite (a âˆˆ I) (t a) univ, fun i => ?_, ?_âŸ©
    Â· by_cases hi : i âˆˆ I
      Â· use t i
        simp_rw [if_pos hi]
        exact âŸ¨Subset.rfl, (h1 i) hiâŸ©
      Â· use univ
        simp_rw [if_neg hi]
        exact âŸ¨Subset.rfl, isOpen_univ, mem_univ _âŸ©
    Â· rw [â† univ_pi_ite]
      simp only [â† ite_and, â† Finset.mem_coe, and_self_iff, univ_pi_ite, h2]

theorem isOpen_pi_iff' [Finite Î¹] {s : Set (âˆ€ a, Ï€ a)} :
    IsOpen s â†”
      âˆ€ f, f âˆˆ s â†’ âˆƒ u : âˆ€ a, Set (Ï€ a), (âˆ€ a, IsOpen (u a) âˆ§ f a âˆˆ u a) âˆ§ univ.pi u âŠ† s := by
  cases nonempty_fintype Î¹
  rw [isOpen_iff_nhds]
  simp_rw [le_principal_iff, nhds_pi, Filter.mem_pi', mem_nhds_iff]
  refine forallâ‚‚_congr fun a _ => âŸ¨?_, ?_âŸ©
  Â· rintro âŸ¨I, t, âŸ¨h1, h2âŸ©âŸ©
    refine
      âŸ¨fun i => (h1 i).choose,
        âŸ¨fun i => (h1 i).choose_spec.2,
          (pi_mono fun i _ => (h1 i).choose_spec.1).trans (Subset.trans ?_ h2)âŸ©âŸ©
    rw [â† pi_inter_compl (I : Set Î¹)]
    exact inter_subset_left
  Â· exact fun âŸ¨u, âŸ¨h1, _âŸ©âŸ© =>
      âŸ¨Finset.univ, u, âŸ¨fun i => âŸ¨u i, âŸ¨rfl.subset, h1 iâŸ©âŸ©, by rwa [Finset.coe_univ]âŸ©âŸ©

theorem isClosed_set_pi {i : Set Î¹} {s : âˆ€ a, Set (Ï€ a)} (hs : âˆ€ a âˆˆ i, IsClosed (s a)) :
    IsClosed (pi i s) := by
  rw [pi_def]; exact isClosed_biInter fun a ha => (hs _ ha).preimage (continuous_apply _)

theorem mem_nhds_of_pi_mem_nhds {I : Set Î¹} {s : âˆ€ i, Set (Ï€ i)} (a : âˆ€ i, Ï€ i) (hs : I.pi s âˆˆ ğ“ a)
    {i : Î¹} (hi : i âˆˆ I) : s i âˆˆ ğ“ (a i) := by
  rw [nhds_pi] at hs; exact mem_of_pi_mem_pi hs hi

theorem set_pi_mem_nhds {i : Set Î¹} {s : âˆ€ a, Set (Ï€ a)} {x : âˆ€ a, Ï€ a} (hi : i.Finite)
    (hs : âˆ€ a âˆˆ i, s a âˆˆ ğ“ (x a)) : pi i s âˆˆ ğ“ x := by
  rw [pi_def, biInter_mem hi]
  exact fun a ha => (continuous_apply a).continuousAt (hs a ha)

theorem set_pi_mem_nhds_iff {I : Set Î¹} (hI : I.Finite) {s : âˆ€ i, Set (Ï€ i)} (a : âˆ€ i, Ï€ i) :
    I.pi s âˆˆ ğ“ a â†” âˆ€ i : Î¹, i âˆˆ I â†’ s i âˆˆ ğ“ (a i) := by
  rw [nhds_pi, pi_mem_pi_iff hI]

theorem interior_pi_set {I : Set Î¹} (hI : I.Finite) {s : âˆ€ i, Set (Ï€ i)} :
    interior (pi I s) = I.pi fun i => interior (s i) := by
  ext a
  simp only [Set.mem_pi, mem_interior_iff_mem_nhds, set_pi_mem_nhds_iff hI]

theorem exists_finset_piecewise_mem_of_mem_nhds [DecidableEq Î¹] {s : Set (âˆ€ a, Ï€ a)} {x : âˆ€ a, Ï€ a}
    (hs : s âˆˆ ğ“ x) (y : âˆ€ a, Ï€ a) : âˆƒ I : Finset Î¹, I.piecewise x y âˆˆ s := by
  simp only [nhds_pi, Filter.mem_pi'] at hs
  rcases hs with âŸ¨I, t, htx, htsâŸ©
  refine âŸ¨I, hts fun i hi => ?_âŸ©
  simpa [Finset.mem_coe.1 hi] using mem_of_mem_nhds (htx i)

theorem pi_generateFrom_eq {Ï€ : Î¹ â†’ Type*} {g : âˆ€ a, Set (Set (Ï€ a))} :
    (@Pi.topologicalSpace Î¹ Ï€ fun a => generateFrom (g a)) =
      generateFrom
        { t | âˆƒ (s : âˆ€ a, Set (Ï€ a)) (i : Finset Î¹), (âˆ€ a âˆˆ i, s a âˆˆ g a) âˆ§ t = pi (â†‘i) s } := by
  refine le_antisymm ?_ ?_
  Â· apply le_generateFrom
    rintro _ âŸ¨s, i, hi, rflâŸ©
    letI := fun a => generateFrom (g a)
    exact isOpen_set_pi i.finite_toSet (fun a ha => GenerateOpen.basic _ (hi a ha))
  Â· classical
    refine le_iInf fun i => coinduced_le_iff_le_induced.1 <| le_generateFrom fun s hs => ?_
    refine GenerateOpen.basic _ âŸ¨update (fun i => univ) i s, {i}, ?_âŸ©
    simp [hs]

theorem pi_eq_generateFrom :
    Pi.topologicalSpace =
      generateFrom
        { g | âˆƒ (s : âˆ€ a, Set (Ï€ a)) (i : Finset Î¹), (âˆ€ a âˆˆ i, IsOpen (s a)) âˆ§ g = pi (â†‘i) s } :=
  calc Pi.topologicalSpace
  _ = @Pi.topologicalSpace Î¹ Ï€ fun a => generateFrom { s | IsOpen s } := by
    simp only [generateFrom_setOf_isOpen]
  _ = _ := pi_generateFrom_eq

theorem pi_generateFrom_eq_finite {Ï€ : Î¹ â†’ Type*} {g : âˆ€ a, Set (Set (Ï€ a))} [Finite Î¹]
    (hg : âˆ€ a, â‹ƒâ‚€ g a = univ) :
    (@Pi.topologicalSpace Î¹ Ï€ fun a => generateFrom (g a)) =
      generateFrom { t | âˆƒ s : âˆ€ a, Set (Ï€ a), (âˆ€ a, s a âˆˆ g a) âˆ§ t = pi univ s } := by
  cases nonempty_fintype Î¹
  rw [pi_generateFrom_eq]
  refine le_antisymm (generateFrom_anti ?_) (le_generateFrom ?_)
  Â· exact fun s âŸ¨t, ht, EqâŸ© => âŸ¨t, Finset.univ, by simp [ht, Eq]âŸ©
  Â· rintro s âŸ¨t, i, ht, rflâŸ©
    letI := generateFrom { t | âˆƒ s : âˆ€ a, Set (Ï€ a), (âˆ€ a, s a âˆˆ g a) âˆ§ t = pi univ s }
    refine isOpen_iff_forall_mem_open.2 fun f hf => ?_
    choose c hcg hfc using fun a => sUnion_eq_univ_iff.1 (hg a) (f a)
    refine âŸ¨pi i t âˆ© pi ((â†‘i)á¶œ : Set Î¹) c, inter_subset_left, ?_, âŸ¨hf, fun a _ => hfc aâŸ©âŸ©
    classical
    rw [â† univ_pi_piecewise]
    refine GenerateOpen.basic _ âŸ¨_, fun a => ?_, rflâŸ©
    by_cases a âˆˆ i <;> simp [*]

theorem induced_to_pi {X : Type*} (f : X â†’ âˆ€ i, Ï€ i) :
    induced f Pi.topologicalSpace = â¨… i, induced (f Â· i) inferInstance := by
  simp_rw [Pi.topologicalSpace, induced_iInf, induced_compose, Function.comp_def]

/-- Suppose `Ï€ i` is a family of topological spaces indexed by `i : Î¹`, and `X` is a type
endowed with a family of maps `f i : X â†’ Ï€ i` for every `i : Î¹`, hence inducing a
map `g : X â†’ Î  i, Ï€ i`. This lemma shows that infimum of the topologies on `X` induced by
the `f i` as `i : Î¹` varies is simply the topology on `X` induced by `g : X â†’ Î  i, Ï€ i`
where `Î  i, Ï€ i` is endowed with the usual product topology. -/
theorem inducing_iInf_to_pi {X : Type*} (f : âˆ€ i, X â†’ Ï€ i) :
    @Inducing X (âˆ€ i, Ï€ i) (â¨… i, induced (f i) inferInstance) _ fun x i => f i x :=
  letI := â¨… i, induced (f i) inferInstance; âŸ¨(induced_to_pi _).symmâŸ©

variable [Finite Î¹] [âˆ€ i, DiscreteTopology (Ï€ i)]

/-- A finite product of discrete spaces is discrete. -/
instance Pi.discreteTopology : DiscreteTopology (âˆ€ i, Ï€ i) :=
  singletons_open_iff_discrete.mp fun x => by
    rw [â† univ_pi_singleton]
    exact isOpen_set_pi finite_univ fun i _ => (isOpen_discrete {x i})

end Pi

section Sigma

variable {Î¹ Îº : Type*} {Ïƒ : Î¹ â†’ Type*} {Ï„ : Îº â†’ Type*} [âˆ€ i, TopologicalSpace (Ïƒ i)]
  [âˆ€ k, TopologicalSpace (Ï„ k)] [TopologicalSpace X]

@[continuity, fun_prop]
theorem continuous_sigmaMk {i : Î¹} : Continuous (@Sigma.mk Î¹ Ïƒ i) :=
  continuous_iSup_rng continuous_coinduced_rng

-- Porting note: the proof was `by simp only [isOpen_iSup_iff, isOpen_coinduced]`
theorem isOpen_sigma_iff {s : Set (Sigma Ïƒ)} : IsOpen s â†” âˆ€ i, IsOpen (Sigma.mk i â»Â¹' s) := by
  delta instTopologicalSpaceSigma
  rw [isOpen_iSup_iff]
  rfl

theorem isClosed_sigma_iff {s : Set (Sigma Ïƒ)} : IsClosed s â†” âˆ€ i, IsClosed (Sigma.mk i â»Â¹' s) := by
  simp only [â† isOpen_compl_iff, isOpen_sigma_iff, preimage_compl]

theorem isOpenMap_sigmaMk {i : Î¹} : IsOpenMap (@Sigma.mk Î¹ Ïƒ i) := by
  intro s hs
  rw [isOpen_sigma_iff]
  intro j
  rcases eq_or_ne j i with (rfl | hne)
  Â· rwa [preimage_image_eq _ sigma_mk_injective]
  Â· rw [preimage_image_sigmaMk_of_ne hne]
    exact isOpen_empty

theorem isOpen_range_sigmaMk {i : Î¹} : IsOpen (range (@Sigma.mk Î¹ Ïƒ i)) :=
  isOpenMap_sigmaMk.isOpen_range

theorem isClosedMap_sigmaMk {i : Î¹} : IsClosedMap (@Sigma.mk Î¹ Ïƒ i) := by
  intro s hs
  rw [isClosed_sigma_iff]
  intro j
  rcases eq_or_ne j i with (rfl | hne)
  Â· rwa [preimage_image_eq _ sigma_mk_injective]
  Â· rw [preimage_image_sigmaMk_of_ne hne]
    exact isClosed_empty

theorem isClosed_range_sigmaMk {i : Î¹} : IsClosed (range (@Sigma.mk Î¹ Ïƒ i)) :=
  isClosedMap_sigmaMk.isClosed_range

theorem openEmbedding_sigmaMk {i : Î¹} : OpenEmbedding (@Sigma.mk Î¹ Ïƒ i) :=
  openEmbedding_of_continuous_injective_open continuous_sigmaMk sigma_mk_injective
    isOpenMap_sigmaMk

theorem closedEmbedding_sigmaMk {i : Î¹} : ClosedEmbedding (@Sigma.mk Î¹ Ïƒ i) :=
  closedEmbedding_of_continuous_injective_closed continuous_sigmaMk sigma_mk_injective
    isClosedMap_sigmaMk

theorem embedding_sigmaMk {i : Î¹} : Embedding (@Sigma.mk Î¹ Ïƒ i) :=
  closedEmbedding_sigmaMk.1

theorem Sigma.nhds_mk (i : Î¹) (x : Ïƒ i) : ğ“ (âŸ¨i, xâŸ© : Sigma Ïƒ) = Filter.map (Sigma.mk i) (ğ“ x) :=
  (openEmbedding_sigmaMk.map_nhds_eq x).symm

theorem Sigma.nhds_eq (x : Sigma Ïƒ) : ğ“ x = Filter.map (Sigma.mk x.1) (ğ“ x.2) := by
  cases x
  apply Sigma.nhds_mk

theorem comap_sigmaMk_nhds (i : Î¹) (x : Ïƒ i) : comap (Sigma.mk i) (ğ“ âŸ¨i, xâŸ©) = ğ“ x :=
  (embedding_sigmaMk.nhds_eq_comap _).symm

theorem isOpen_sigma_fst_preimage (s : Set Î¹) : IsOpen (Sigma.fst â»Â¹' s : Set (Î£ a, Ïƒ a)) := by
  rw [â† biUnion_of_singleton s, preimage_iUnionâ‚‚]
  simp only [â† range_sigmaMk]
  exact isOpen_biUnion fun _ _ => isOpen_range_sigmaMk

/-- A map out of a sum type is continuous iff its restriction to each summand is. -/
@[simp]
theorem continuous_sigma_iff {f : Sigma Ïƒ â†’ X} :
    Continuous f â†” âˆ€ i, Continuous fun a => f âŸ¨i, aâŸ© := by
  delta instTopologicalSpaceSigma
  rw [continuous_iSup_dom]
  exact forall_congr' fun _ => continuous_coinduced_dom

/-- A map out of a sum type is continuous if its restriction to each summand is. -/
@[continuity, fun_prop]
theorem continuous_sigma {f : Sigma Ïƒ â†’ X} (hf : âˆ€ i, Continuous fun a => f âŸ¨i, aâŸ©) :
    Continuous f :=
  continuous_sigma_iff.2 hf

/-- A map defined on a sigma type (a.k.a. the disjoint union of an indexed family of topological
spaces) is inducing iff its restriction to each component is inducing and each the image of each
component under `f` can be separated from the images of all other components by an open set. -/
theorem inducing_sigma {f : Sigma Ïƒ â†’ X} :
    Inducing f â†” (âˆ€ i, Inducing (f âˆ˜ Sigma.mk i)) âˆ§
      (âˆ€ i, âˆƒ U, IsOpen U âˆ§ âˆ€ x, f x âˆˆ U â†” x.1 = i) := by
  refine âŸ¨fun h â†¦ âŸ¨fun i â†¦ h.comp embedding_sigmaMk.1, fun i â†¦ ?_âŸ©, ?_âŸ©
  Â· rcases h.isOpen_iff.1 (isOpen_range_sigmaMk (i := i)) with âŸ¨U, hUo, hUâŸ©
    refine âŸ¨U, hUo, ?_âŸ©
    simpa [Set.ext_iff] using hU
  Â· refine fun âŸ¨hâ‚, hâ‚‚âŸ© â†¦ inducing_iff_nhds.2 fun âŸ¨i, xâŸ© â†¦ ?_
    rw [Sigma.nhds_mk, (hâ‚ i).nhds_eq_comap, comp_apply, â† comap_comap, map_comap_of_mem]
    rcases hâ‚‚ i with âŸ¨U, hUo, hUâŸ©
    filter_upwards [preimage_mem_comap <| hUo.mem_nhds <| (hU _).2 rfl] with y hy
    simpa [hU] using hy

@[simp 1100]
theorem continuous_sigma_map {fâ‚ : Î¹ â†’ Îº} {fâ‚‚ : âˆ€ i, Ïƒ i â†’ Ï„ (fâ‚ i)} :
    Continuous (Sigma.map fâ‚ fâ‚‚) â†” âˆ€ i, Continuous (fâ‚‚ i) :=
  continuous_sigma_iff.trans <| by simp only [Sigma.map, embedding_sigmaMk.continuous_iff, comp_def]

@[continuity, fun_prop]
theorem Continuous.sigma_map {fâ‚ : Î¹ â†’ Îº} {fâ‚‚ : âˆ€ i, Ïƒ i â†’ Ï„ (fâ‚ i)} (hf : âˆ€ i, Continuous (fâ‚‚ i)) :
    Continuous (Sigma.map fâ‚ fâ‚‚) :=
  continuous_sigma_map.2 hf

theorem isOpenMap_sigma {f : Sigma Ïƒ â†’ X} : IsOpenMap f â†” âˆ€ i, IsOpenMap fun a => f âŸ¨i, aâŸ© := by
  simp only [isOpenMap_iff_nhds_le, Sigma.forall, Sigma.nhds_eq, map_map, comp_def]

theorem isOpenMap_sigma_map {fâ‚ : Î¹ â†’ Îº} {fâ‚‚ : âˆ€ i, Ïƒ i â†’ Ï„ (fâ‚ i)} :
    IsOpenMap (Sigma.map fâ‚ fâ‚‚) â†” âˆ€ i, IsOpenMap (fâ‚‚ i) :=
  isOpenMap_sigma.trans <|
    forall_congr' fun i => (@openEmbedding_sigmaMk _ _ _ (fâ‚ i)).isOpenMap_iff.symm

theorem inducing_sigma_map {fâ‚ : Î¹ â†’ Îº} {fâ‚‚ : âˆ€ i, Ïƒ i â†’ Ï„ (fâ‚ i)} (hâ‚ : Injective fâ‚) :
    Inducing (Sigma.map fâ‚ fâ‚‚) â†” âˆ€ i, Inducing (fâ‚‚ i) := by
  simp only [inducing_iff_nhds, Sigma.forall, Sigma.nhds_mk, Sigma.map_mk, â† map_sigma_mk_comap hâ‚,
    map_inj sigma_mk_injective]

theorem embedding_sigma_map {fâ‚ : Î¹ â†’ Îº} {fâ‚‚ : âˆ€ i, Ïƒ i â†’ Ï„ (fâ‚ i)} (h : Injective fâ‚) :
    Embedding (Sigma.map fâ‚ fâ‚‚) â†” âˆ€ i, Embedding (fâ‚‚ i) := by
  simp only [embedding_iff, Injective.sigma_map, inducing_sigma_map h, forall_and, h.sigma_map_iff]

theorem openEmbedding_sigma_map {fâ‚ : Î¹ â†’ Îº} {fâ‚‚ : âˆ€ i, Ïƒ i â†’ Ï„ (fâ‚ i)} (h : Injective fâ‚) :
    OpenEmbedding (Sigma.map fâ‚ fâ‚‚) â†” âˆ€ i, OpenEmbedding (fâ‚‚ i) := by
  simp only [openEmbedding_iff_embedding_open, isOpenMap_sigma_map, embedding_sigma_map h,
    forall_and]

end Sigma

section ULift

theorem ULift.isOpen_iff [TopologicalSpace X] {s : Set (ULift.{v} X)} :
    IsOpen s â†” IsOpen (ULift.up â»Â¹' s) := by
  rw [ULift.topologicalSpace, â† Equiv.ulift_apply, â† Equiv.ulift.coinduced_symm, â† isOpen_coinduced]

theorem ULift.isClosed_iff [TopologicalSpace X] {s : Set (ULift.{v} X)} :
    IsClosed s â†” IsClosed (ULift.up â»Â¹' s) := by
  rw [â† isOpen_compl_iff, â† isOpen_compl_iff, isOpen_iff, preimage_compl]

@[continuity]
theorem continuous_uLift_down [TopologicalSpace X] : Continuous (ULift.down : ULift.{v, u} X â†’ X) :=
  continuous_induced_dom

@[continuity]
theorem continuous_uLift_up [TopologicalSpace X] : Continuous (ULift.up : X â†’ ULift.{v, u} X) :=
  continuous_induced_rng.2 continuous_id

theorem embedding_uLift_down [TopologicalSpace X] : Embedding (ULift.down : ULift.{v, u} X â†’ X) :=
  âŸ¨âŸ¨rflâŸ©, ULift.down_injectiveâŸ©

theorem ULift.closedEmbedding_down [TopologicalSpace X] :
    ClosedEmbedding (ULift.down : ULift.{v, u} X â†’ X) :=
  âŸ¨embedding_uLift_down, by simp only [ULift.down_surjective.range_eq, isClosed_univ]âŸ©

instance [TopologicalSpace X] [DiscreteTopology X] : DiscreteTopology (ULift X) :=
  embedding_uLift_down.discreteTopology

end ULift

section Monad

variable [TopologicalSpace X] {s : Set X} {t : Set s}

theorem IsOpen.trans (ht : IsOpen t) (hs : IsOpen s) : IsOpen (t : Set X) := by
  rcases isOpen_induced_iff.mp ht with âŸ¨s', hs', rflâŸ©
  rw [Subtype.image_preimage_coe]
  exact hs.inter hs'

theorem IsClosed.trans (ht : IsClosed t) (hs : IsClosed s) : IsClosed (t : Set X) := by
  rcases isClosed_induced_iff.mp ht with âŸ¨s', hs', rflâŸ©
  rw [Subtype.image_preimage_coe]
  exact hs.inter hs'

end Monad

section NhdsSet
variable {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] {f : Filter X}
  {s : Set X} {t : Set Y} {x : X}

/-- The product of a neighborhood of `s` and a neighborhood of `t` is a neighborhood of `s Ã—Ë¢ t`,
formulated in terms of a filter inequality. -/
theorem nhdsSet_prod_le (s : Set X) (t : Set Y) : ğ“Ë¢ (s Ã—Ë¢ t) â‰¤ ğ“Ë¢ s Ã—Ë¢ ğ“Ë¢ t :=
  ((hasBasis_nhdsSet _).prod (hasBasis_nhdsSet _)).ge_iff.2 fun (_u, _v) âŸ¨âŸ¨huo, hsuâŸ©, hvo, htvâŸ© â†¦
    (huo.prod hvo).mem_nhdsSet.2 <| prod_mono hsu htv

theorem Filter.eventually_nhdsSet_prod_iff {p : X Ã— Y â†’ Prop} :
    (âˆ€á¶  q in ğ“Ë¢ (s Ã—Ë¢ t), p q) â†”
      âˆ€ x âˆˆ s, âˆ€ y âˆˆ t,
          âˆƒ px : X â†’ Prop, (âˆ€á¶  x' in ğ“ x, px x') âˆ§ âˆƒ py : Y â†’ Prop, (âˆ€á¶  y' in ğ“ y, py y') âˆ§
            âˆ€ {x : X}, px x â†’ âˆ€ {y : Y}, py y â†’ p (x, y) := by
  simp_rw [eventually_nhdsSet_iff_forall, forall_prod_set, nhds_prod_eq, eventually_prod_iff]

theorem Filter.Eventually.prod_nhdsSet {p : X Ã— Y â†’ Prop} {px : X â†’ Prop} {py : Y â†’ Prop}
    (hp : âˆ€ {x : X}, px x â†’ âˆ€ {y : Y}, py y â†’ p (x, y)) (hs : âˆ€á¶  x in ğ“Ë¢ s, px x)
    (ht : âˆ€á¶  y in ğ“Ë¢ t, py y) : âˆ€á¶  q in ğ“Ë¢ (s Ã—Ë¢ t), p q :=
  nhdsSet_prod_le _ _ (mem_of_superset (prod_mem_prod hs ht) fun _ âŸ¨hx, hyâŸ© â†¦ hp hx hy)

end NhdsSet

set_option linter.style.longFile 1700
