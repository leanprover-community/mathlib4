/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Yury Kudryashov
-/
module

public import Mathlib.Topology.ExtendFrom
public import Mathlib.Topology.Order.DenselyOrdered

/-!
# Lemmas about `extendFrom` in an order topology.
-/

public section

open Filter Set Topology

variable {Î± Î² : Type*} [TopologicalSpace Î±] [LinearOrder Î±] [DenselyOrdered Î±] [OrderTopology Î±]
  [TopologicalSpace Î²] {f : Î± â†’ Î²} {a b : Î±} {la lb : Î²}

section RegularSpace

variable [RegularSpace Î²]

theorem continuousOn_Icc_extendFrom_Ioo
    (hab : a â‰  b) (hf : ContinuousOn f (Ioo a b)) (ha : Tendsto f (ğ“[>] a) (ğ“ la))
    (hb : Tendsto f (ğ“[<] b) (ğ“ lb)) : ContinuousOn (extendFrom (Ioo a b) f) (Icc a b) := by
  apply continuousOn_extendFrom
  Â· rw [closure_Ioo hab]
  Â· intro x x_in
    rcases eq_endpoints_or_mem_Ioo_of_mem_Icc x_in with (rfl | rfl | h)
    Â· exact âŸ¨la, ha.mono_left <| nhdsWithin_mono _ Ioo_subset_Ioi_selfâŸ©
    Â· exact âŸ¨lb, hb.mono_left <| nhdsWithin_mono _ Ioo_subset_Iio_selfâŸ©
    Â· exact âŸ¨f x, hf x hâŸ©

theorem continuousOn_uIcc_extendFrom_uIoo
    (hab : a â‰  b) (hf : ContinuousOn f (uIoo a b))
    (ha : Tendsto f (ğ“[uIoo a b] a) (ğ“ la)) (hb : Tendsto f (ğ“[uIoo a b] b) (ğ“ lb)) :
    ContinuousOn (extendFrom (uIoo a b) f) (uIcc a b) := by
  obtain hab' | hba' := hab.lt_or_gt
  Â· simp only [hab', uIoo_of_lt, nhdsWithin_Ioo_eq_nhdsGT, nhdsWithin_Ioo_eq_nhdsLT,
      uIcc_of_lt] at ha hb hf âŠ¢
    exact continuousOn_Icc_extendFrom_Ioo hab hf ha hb
  Â· simp only [hba', uIoo_of_gt, nhdsWithin_Ioo_eq_nhdsGT, nhdsWithin_Ioo_eq_nhdsLT,
      uIcc_of_gt] at ha hb hf âŠ¢
    exact continuousOn_Icc_extendFrom_Ioo hab.symm hf hb ha

theorem continuousOn_Ico_extendFrom_Ioo
    (hab : a < b) (hf : ContinuousOn f (Ioo a b)) (ha : Tendsto f (ğ“[>] a) (ğ“ la)) :
    ContinuousOn (extendFrom (Ioo a b) f) (Ico a b) := by
  apply continuousOn_extendFrom
  Â· rw [closure_Ioo hab.ne]
    exact Ico_subset_Icc_self
  Â· intro x x_in
    rcases eq_left_or_mem_Ioo_of_mem_Ico x_in with (rfl | h)
    Â· use la
      simpa [hab]
    Â· exact âŸ¨f x, hf x hâŸ©

theorem continuousOn_Ioc_extendFrom_Ioo
    (hab : a < b) (hf : ContinuousOn f (Ioo a b)) (hb : Tendsto f (ğ“[<] b) (ğ“ lb)) :
    ContinuousOn (extendFrom (Ioo a b) f) (Ioc a b) := by
  have := continuousOn_Ico_extendFrom_Ioo (f := f âˆ˜ OrderDual.ofDual) (la := lb) hab.dual
  rw [Ico_toDual, Ioi_toDual, Ioo_toDual] at this
  exact this hf hb

end RegularSpace

section T2Space

variable [T2Space Î²]

theorem eq_lim_at_left_extendFrom_Ioo (hab : a < b)
    (ha : Tendsto f (ğ“[>] a) (ğ“ la)) : extendFrom (Ioo a b) f a = la := by
  apply extendFrom_eq
  Â· rw [closure_Ioo hab.ne]
    simp only [le_of_lt hab, left_mem_Icc]
  Â· simpa [hab]

theorem eq_lim_at_right_extendFrom_Ioo (hab : a < b)
    (hb : Tendsto f (ğ“[<] b) (ğ“ lb)) : extendFrom (Ioo a b) f b = lb := by
  apply extendFrom_eq
  Â· rw [closure_Ioo hab.ne]
    simp only [le_of_lt hab, right_mem_Icc]
  Â· simpa [hab]

theorem eq_lim_at_left_extendFrom_uIoo (hab : a â‰  b)
    (ha : Tendsto f (ğ“[uIoo a b] a) (ğ“ la)) : extendFrom (uIoo a b) f a = la :=
  extendFrom_eq (by simp [hab]) ha

theorem eq_lim_at_right_extendFrom_uIoo (hab : a â‰  b)
    (hb : Tendsto f (ğ“[uIoo a b] b) (ğ“ lb)) : extendFrom (uIoo a b) f b = lb :=
  extendFrom_eq (by simp [hab]) hb

end T2Space
