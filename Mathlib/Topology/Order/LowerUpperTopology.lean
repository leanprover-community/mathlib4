/-
Copyright (c) 2023 Christopher Hoskin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christopher Hoskin
-/
import Mathlib.Topology.Homeomorph
import Mathlib.Topology.Order.Lattice
import Mathlib.Order.Hom.CompleteLattice

#align_import topology.order.lower_topology from "leanprover-community/mathlib"@"98e83c3d541c77cdb7da20d79611a780ff8e7d90"

/-!
# Lower and Upper topology

This file introduces the lower topology on a preorder as the topology generated by the complements
of the left-closed right-infinite intervals.

For completeness we also introduce the dual upper topology, generated by the complements of the
right-closed left-infinite intervals.

## Main statements

- `Topology.IsLower.t0Space` - the lower topology on a partial order is T₀
- `Topology.IsLower.isTopologicalBasis` - the complements of the upper closures of finite
  subsets form a basis for the lower topology
- `Topology.IsLower.continuousInf` - the inf map is continuous with respect to the lower topology

## Implementation notes

A type synonym `Topology.WithLower` is introduced and for a preorder `α`, `Topology.WithLower α`
is made an instance of `TopologicalSpace` by the topology generated by the complements of the
closed intervals to infinity.

We define a mixin class `Topology.IsLower` for the class of types which are both a preorder and a
topology and where the topology is generated by the complements of the closed intervals to infinity.
It is shown that `Topology.WithLower α` is an instance of `Topology.IsLower`.

Similarly for the upper topology.

## Motivation

The lower topology is used with the `Scott` topology to define the Lawson topology. The restriction
of the lower topology to the spectrum of a complete lattice coincides with the hull-kernel topology.

## References

* [Gierz et al, *A Compendium of Continuous Lattices*][GierzEtAl1980]

## Tags

lower topology, upper topology, preorder
-/

set_option autoImplicit true

open Set TopologicalSpace

section preorder

variable (α : Type*) [Preorder α]

def Topology.lower : TopologicalSpace α := generateFrom { s | ∃ a, (Ici a)ᶜ = s }

def Topology.upper : TopologicalSpace α := generateFrom { s | ∃ a, (Iic a)ᶜ = s }

end preorder

section Topology.WithLower

variable (α β : Type*)

/-- Type synonym for a preorder equipped with the lower topology. -/
def Topology.WithLower := α
#align with_lower_topology Topology.WithLower

variable {α β}

namespace Topology.WithLower

/-- `toLower` is the identity function to the `Topology.WithLower` of a type.  -/
@[match_pattern]
def toLower : α ≃ Topology.WithLower α := Equiv.refl _
#align with_lower_topology.to_lower Topology.WithLower.toLower

/-- `ofLower` is the identity function from the `Topology.WithLower` of a type.  -/
@[match_pattern]
def ofLower : Topology.WithLower α ≃ α := Equiv.refl _
#align with_lower_topology.of_lower Topology.WithLower.ofLower

@[simp]
theorem to_Topology.WithLower_symm_eq : (@toLower α).symm = ofLower :=
  rfl
#align with_lower_topology.to_with_lower_topology_symm_eq Topology.WithLower.to_Topology.WithLower_symm_eq

@[simp]
theorem of_Topology.WithLower_symm_eq : (@ofLower α).symm = toLower :=
  rfl
#align with_lower_topology.of_with_lower_topology_symm_eq Topology.WithLower.of_Topology.WithLower_symm_eq

@[simp]
theorem toLower_ofLower (a : Topology.WithLower α) : toLower (ofLower a) = a :=
  rfl
#align with_lower_topology.to_lower_of_lower Topology.WithLower.toLower_ofLower

@[simp]
theorem ofLower_toLower (a : α) : ofLower (toLower a) = a :=
  rfl
#align with_lower_topology.of_lower_to_lower Topology.WithLower.ofLower_toLower

-- porting note: removed @[simp] to make linter happy
theorem toLower_inj {a b : α} : toLower a = toLower b ↔ a = b :=
  Iff.rfl
#align with_lower_topology.to_lower_inj Topology.WithLower.toLower_inj

-- porting note: removed @[simp] to make linter happy
theorem ofLower_inj {a b : Topology.WithLower α} : ofLower a = ofLower b ↔ a = b :=
  Iff.rfl
#align with_lower_topology.of_lower_inj Topology.WithLower.ofLower_inj

/-- A recursor for `Topology.WithLower`. Use as `induction x using Topology.WithLower.rec`. -/
protected def rec {β : Topology.WithLower α → Sort*} (h : ∀ a, β (toLower a)) : ∀ a, β a := fun a =>
  h (ofLower a)
#align with_lower_topology.rec Topology.WithLower.rec

instance [Nonempty α] : Nonempty (Topology.WithLower α) :=
  ‹Nonempty α›

instance [Inhabited α] : Inhabited (Topology.WithLower α) :=
  ‹Inhabited α›

variable [Preorder α]

instance : Preorder (Topology.WithLower α) :=
  ‹Preorder α›

instance : TopologicalSpace (Topology.WithLower α) := Topology.lower α

theorem isOpen_preimage_ofLower (S : Set α) :
    IsOpen (Topology.WithLower.ofLower ⁻¹' S) ↔
      (Topology.lower α).IsOpen S :=
  Iff.rfl
#align with_lower_topology.is_open_preimage_of_lower Topology.WithLower.isOpen_preimage_ofLower

theorem isOpen_def (T : Set (Topology.WithLower α)) :
    IsOpen T ↔ (Topology.lower α).IsOpen
    (Topology.WithLower.toLower ⁻¹' T) :=
  Iff.rfl
#align with_lower_topology.is_open_def Topology.WithLower.isOpen_def

end Topology.WithLower

end Topology.WithLower

section Topology.WithUpper

variable (α β : Type*)

/-- Type synonym for a preorder equipped with the upper topology. -/
def Topology.WithUpper := α

variable {α β}

namespace Topology.WithUpper

/-- `toUpper` is the identity function to the `Topology.WithUpper` of a type.  -/
@[match_pattern]
def toUpper : α ≃ Topology.WithUpper α := Equiv.refl _

/-- `ofUpper` is the identity function from the `Topology.WithUpper` of a type.  -/
@[match_pattern]
def ofUpper : Topology.WithUpper α ≃ α := Equiv.refl _

@[simp]
theorem to_Topology.WithUpper_symm_eq {α} : (@toUpper α).symm = ofUpper :=
  rfl

@[simp]
theorem of_Topology.WithUpper_symm_eq : (@ofUpper α).symm = toUpper :=
  rfl

@[simp]
theorem toUpper_ofUpper (a : Topology.WithUpper α) : toUpper (ofUpper a) = a :=
  rfl

@[simp]
theorem ofUpper_toUpper (a : α) : ofUpper (toUpper a) = a :=
  rfl

theorem toUpper_inj {a b : α} : toUpper a = toUpper b ↔ a = b :=
  Iff.rfl

theorem ofUpper_inj {a b : Topology.WithUpper α} : ofUpper a = ofUpper b ↔ a = b :=
  Iff.rfl

/-- A recursor for `Topology.WithUpper`. Use as `induction x using Topology.WithUpper.rec`. -/
protected def rec {β : Topology.WithUpper α → Sort*} (h : ∀ a, β (toUpper a)) : ∀ a, β a := fun a =>
  h (ofUpper a)

instance [Nonempty α] : Nonempty (Topology.WithUpper α) :=
  ‹Nonempty α›

instance [Inhabited α] : Inhabited (Topology.WithUpper α) :=
  ‹Inhabited α›

variable [Preorder α]

instance : Preorder (Topology.WithUpper α) :=
  ‹Preorder α›

instance : TopologicalSpace (Topology.WithUpper α) :=
  Topology.upper α

theorem isOpen_preimage_ofUpper (S : Set α) :
    IsOpen (Topology.WithUpper.ofUpper ⁻¹' S) ↔
      (Topology.upper α).IsOpen S :=
  Iff.rfl

theorem isOpen_def (T : Set (Topology.WithUpper α)) :
    IsOpen T ↔ (Topology.upper α).IsOpen
    (Topology.WithUpper.toUpper ⁻¹' T) :=
  Iff.rfl

end Topology.WithUpper

end Topology.WithUpper

/--
The lower topology is the topology generated by the complements of the left-closed right-infinite
intervals.
-/
class Topology.IsLower (α : Type*) [t : TopologicalSpace α] [Preorder α] : Prop where
  topology_eq_lowerTopology : t = Topology.lower α
#align lower_topology Topology.IsLower

attribute [nolint docBlame] Topology.IsLower.topology_eq_lowerTopology

/--
The upper topology is the topology generated by the complements of the right-closed left-infinite
intervals.
-/
class Topology.IsUpper (α : Type*) [t : TopologicalSpace α] [Preorder α] : Prop where
  topology_eq_upperTopology : t = Topology.upper α
attribute [nolint docBlame] Topology.IsUpper.topology_eq_upperTopology

instance [Preorder α] : Topology.IsLower (Topology.WithLower α) :=
  ⟨rfl⟩

instance [Preorder α] : Topology.IsUpper (Topology.WithUpper α) :=
  ⟨rfl⟩

/--
The lower topology is homeomorphic to the upper topology on the dual order
-/
def Topology.WithLower.toDualHomeomorph [Preorder α] :
    Topology.WithLower α ≃ₜ Topology.WithUpper αᵒᵈ where
  toFun := OrderDual.toDual
  invFun := OrderDual.ofDual
  left_inv := OrderDual.toDual_ofDual
  right_inv := OrderDual.ofDual_toDual
  continuous_toFun := continuous_coinduced_rng
  continuous_invFun := continuous_coinduced_rng

namespace Topology.IsLower

/-- The complements of the upper closures of finite sets are a collection of lower sets
which form a basis for the lower topology. -/
def lowerBasis (α : Type*) [Preorder α] :=
  { s : Set α | ∃ t : Set α, t.Finite ∧ (upperClosure t : Set α)ᶜ = s }
#align lower_topology.lower_basis Topology.IsLower.lowerBasis

section Preorder

variable (α)
variable [Preorder α] [TopologicalSpace α] [Topology.IsLower α] {s : Set α}

lemma topology_eq : ‹_› = Topology.lower α := topology_eq_lowerTopology

variable {α}

/-- If `α` is equipped with the lower topology, then it is homeomorphic to `Topology.WithLower α`.
-/
def Topology.WithLowerHomeomorph : Topology.WithLower α ≃ₜ α :=
  Topology.WithLower.ofLower.toHomeomorphOfInducing ⟨by erw [topology_eq α, induced_id]; rfl⟩
#align lower_topology.with_lower_topology_homeomorph Topology.IsLower.Topology.WithLowerHomeomorph

theorem isOpen_iff_generate_Ici_compl : IsOpen s ↔ GenerateOpen { t | ∃ a, (Ici a)ᶜ = t } s := by
  rw [topology_eq α]; rfl
#align lower_topology.is_open_iff_generate_Ici_compl Topology.IsLower.isOpen_iff_generate_Ici_compl

instance instTopology.IsUpperDual [Preorder α] [TopologicalSpace α] [Topology.IsLower α] :
    Topology.IsUpper (αᵒᵈ) where
  topology_eq_upperTopology := topology_eq_lowerTopology (α := α)

/-- Left-closed right-infinite intervals [a, ∞) are closed in the lower topology. -/
instance : ClosedIciTopology α :=
  ⟨fun a ↦ isOpen_compl_iff.1 <| isOpen_iff_generate_Ici_compl.2 <| GenerateOpen.basic _ ⟨a, rfl⟩⟩

-- Porting note: The old `Topology.IsLower.isClosed_Ici` was removed, since one can now use
-- the general `isClosed_Ici` lemma thanks to the instance above.
#align lower_topology.is_closed_Ici isClosed_Ici

/-- The upper closure of a finite set is closed in the lower topology. -/
theorem isClosed_upperClosure (h : s.Finite) : IsClosed (upperClosure s : Set α) := by
  simp only [← UpperSet.iInf_Ici, UpperSet.coe_iInf]
  exact h.isClosed_biUnion fun _ _ => isClosed_Ici
#align lower_topology.is_closed_upper_closure Topology.IsLower.isClosed_upperClosure

/-- Every set open in the lower topology is a lower set. -/
theorem isLowerSet_of_isOpen (h : IsOpen s) : IsLowerSet s := by
  -- porting note: `rw` leaves a shadowed assumption
  replace h := isOpen_iff_generate_Ici_compl.1 h
  induction h
  case basic u h' => obtain ⟨a, rfl⟩ := h'; exact (isUpperSet_Ici a).compl
  case univ => exact isLowerSet_univ
  case inter u v _ _ hu2 hv2 => exact hu2.inter hv2
  case sUnion _ _ ih => exact isLowerSet_sUnion ih
#align lower_topology.is_lower_set_of_is_open Topology.IsLower.isLowerSet_of_isOpen

theorem isUpperSet_of_isClosed (h : IsClosed s) : IsUpperSet s :=
  isLowerSet_compl.1 <| isLowerSet_of_isOpen h.isOpen_compl
#align lower_topology.is_upper_set_of_is_closed Topology.IsLower.isUpperSet_of_isClosed

/--
The closure of a singleton `{a}` in the lower topology is the left-closed right-infinite interval
[a, ∞).
-/
@[simp]
theorem closure_singleton (a : α) : closure {a} = Ici a :=
  Subset.antisymm ((closure_minimal fun _ h => h.ge) <| isClosed_Ici) <|
    (isUpperSet_of_isClosed isClosed_closure).Ici_subset <| subset_closure rfl
#align lower_topology.closure_singleton Topology.IsLower.closure_singleton

protected theorem isTopologicalBasis : IsTopologicalBasis (lowerBasis α) := by
  convert isTopologicalBasis_of_subbasis (topology_eq α)
  simp_rw [lowerBasis, coe_upperClosure, compl_iUnion]
  ext s
  constructor
  · rintro ⟨F, hF, rfl⟩
    refine' ⟨(fun a => (Ici a)ᶜ) '' F, ⟨hF.image _, image_subset_iff.2 fun _ _ => ⟨_, rfl⟩⟩, _⟩
    simp only [sInter_image]
  · rintro ⟨F, ⟨hF, hs⟩, rfl⟩
    haveI := hF.to_subtype
    rw [subset_def, Subtype.forall'] at hs
    choose f hf using hs
    exact ⟨_, finite_range f, by simp_rw [biInter_range, hf, sInter_eq_iInter]⟩
#align lower_topology.is_topological_basis Topology.IsLower.isTopologicalBasis

/-- A function `f : β → α` with lower topology in the codomain is continuous provided that the
preimage of every interval `Set.Ici a` is a closed set.

TODO: upgrade to an `iff`. -/
lemma continuous_of_Ici [TopologicalSpace β] {f : β → α} (h : ∀ a, IsClosed (f ⁻¹' (Ici a))) :
    Continuous f := by
  obtain rfl := Topology.IsLower.topology_eq α
  refine continuous_generateFrom ?_
  rintro _ ⟨a, rfl⟩
  exact (h a).isOpen_compl

end Preorder

section PartialOrder

variable [PartialOrder α] [TopologicalSpace α] [Topology.IsLower α]

-- see Note [lower instance priority]
/-- The lower topology on a partial order is T₀. -/
instance (priority := 90) t0Space : T0Space α :=
  (t0Space_iff_inseparable α).2 fun x y h =>
    Ici_injective <| by simpa only [inseparable_iff_closure_eq, closure_singleton] using h

end PartialOrder

end Topology.IsLower


namespace Topology.IsUpper

/-- The complements of the lower closures of finite sets are a collection of upper sets
which form a basis for the upper topology. -/
def upperBasis (α : Type*) [Preorder α] :=
  { s : Set α | ∃ t : Set α, t.Finite ∧ (lowerClosure t : Set α)ᶜ = s }

section Preorder

variable (α)
variable [Preorder α] [TopologicalSpace α] [Topology.IsUpper α] {s : Set α}

lemma topology_eq : ‹_› = Topology.upper α := topology_eq_upperTopology

variable {α}

/-- If `α` is equipped with the upper topology, then it is homeomorphic to `Topology.WithUpper α`.
-/
def Topology.WithUpperHomeomorph : Topology.WithUpper α ≃ₜ α :=
  Topology.WithUpper.ofUpper.toHomeomorphOfInducing ⟨by erw [topology_eq α, induced_id]; rfl⟩

theorem isOpen_iff_generate_Iic_compl : IsOpen s ↔ GenerateOpen { t | ∃ a, (Iic a)ᶜ = t } s := by
  rw [topology_eq α]; rfl

instance instTopology.IsLowerDual [Preorder α] [TopologicalSpace α] [Topology.IsUpper α] :
    Topology.IsLower (αᵒᵈ) where
  topology_eq_lowerTopology := topology_eq_upperTopology (α := α)

/-- Left-infinite right-closed intervals (-∞,a] are closed in the upper topology. -/
instance : ClosedIicTopology α :=
  ⟨fun a ↦ isOpen_compl_iff.1 <| isOpen_iff_generate_Iic_compl.2 <| GenerateOpen.basic _ ⟨a, rfl⟩⟩

/-- The lower closure of a finite set is closed in the upper topology. -/
theorem isClosed_lowerClosure (h : s.Finite) : IsClosed (lowerClosure s : Set α) :=
  Topology.IsLower.isClosed_upperClosure (α := αᵒᵈ) h

/-- Every set open in the upper topology is a upper set. -/
theorem isUpperSet_of_isOpen (h : IsOpen s) : IsUpperSet s :=
  Topology.IsLower.isLowerSet_of_isOpen (α := αᵒᵈ) h

theorem isLowerSet_of_isClosed (h : IsClosed s) : IsLowerSet s :=
  isUpperSet_compl.1 <| isUpperSet_of_isOpen h.isOpen_compl

/--
The closure of a singleton `{a}` in the upper topology is the left-infinite right-closed interval
(-∞,a].
-/
@[simp]
theorem closure_singleton (a : α) : closure {a} = Iic a :=
  Topology.IsLower.closure_singleton (α := αᵒᵈ) _

protected theorem isTopologicalBasis : IsTopologicalBasis (upperBasis α) :=
  Topology.IsLower.isTopologicalBasis (α := αᵒᵈ)

/-- A function `f : β → α` with upper topology in the codomain is continuous provided that the
preimage of every interval `Set.Iic a` is a closed set.

TODO: upgrade to an `iff`. -/
lemma continuous_of_Iic [TopologicalSpace β] {f : β → α} (h : ∀ a, IsClosed (f ⁻¹' (Iic a))) :
    Continuous f :=
  Topology.IsLower.continuous_of_Ici (α := αᵒᵈ) h

end Preorder


section PartialOrder

variable [PartialOrder α] [TopologicalSpace α] [Topology.IsUpper α]

-- see Note [lower instance priority]
/-- The upper topology on a partial order is T₀. -/
instance (priority := 90) t0Space : T0Space α :=
  Topology.IsLower.t0Space (α := αᵒᵈ)

end PartialOrder

end Topology.IsUpper

instance instTopology.IsLowerProd [Preorder α] [TopologicalSpace α] [Topology.IsLower α]
    [OrderBot α] [Preorder β] [TopologicalSpace β] [Topology.IsLower β] [OrderBot β] :
    Topology.IsLower (α × β) where
  topology_eq_lowerTopology := by
    refine' le_antisymm (le_generateFrom _) _
    · rintro _ ⟨x, rfl⟩
      exact (isClosed_Ici.prod isClosed_Ici).isOpen_compl
    rw [(Topology.IsLower.isTopologicalBasis.prod
      Topology.IsLower.isTopologicalBasis).eq_generateFrom, le_generateFrom_iff_subset_isOpen,
      image2_subset_iff]
    rintro _ ⟨s, hs, rfl⟩ _ ⟨t, ht, rfl⟩
    dsimp
    simp_rw [coe_upperClosure, compl_iUnion, prod_eq, preimage_iInter, preimage_compl]
    -- without `let`, `refine` tries to use the product topology and fails
    let _ : TopologicalSpace (α × β) := Topology.lower (α × β)
    refine (hs.isOpen_biInter fun a _ => ?_).inter (ht.isOpen_biInter fun b _ => ?_)
    · exact GenerateOpen.basic _ ⟨(a, ⊥), by simp [Ici_prod_eq, prod_univ]⟩
    · exact GenerateOpen.basic _ ⟨(⊥, b), by simp [Ici_prod_eq, univ_prod]⟩

instance instTopology.IsUpperProd [Preorder α] [TopologicalSpace α] [Topology.IsUpper α]
    [OrderTop α] [Preorder β] [TopologicalSpace β] [Topology.IsUpper β] [OrderTop β] :
    Topology.IsUpper (α × β) where
  topology_eq_upperTopology := by
    suffices : Topology.IsLower (α × β)ᵒᵈ
    · exact Topology.IsLower.topology_eq_lowerTopology (α := (α × β)ᵒᵈ)
    exact instTopology.IsLowerProd (α := αᵒᵈ) (β := βᵒᵈ)

section CompleteLattice_Topology.IsLower

variable [CompleteLattice α] [CompleteLattice β] [TopologicalSpace α] [Topology.IsLower α]
  [TopologicalSpace β] [Topology.IsLower β]

protected theorem sInfHom.continuous (f : sInfHom α β) : Continuous f := by
  refine Topology.IsLower.continuous_of_Ici fun b => ?_
  convert isClosed_Ici (a := sInf <| f ⁻¹' Ici b)
  refine' Subset.antisymm (fun a => sInf_le) fun a ha => le_trans _ <|
    OrderHomClass.mono (f : α →o β) ha
  refine' LE.le.trans _ (map_sInf f _).ge
  simp
#align Inf_hom.continuous sInfHom.continuous

-- see Note [lower instance priority]
instance (priority := 90) Topology.IsLower.continuousInf : ContinuousInf α :=
  ⟨(infsInfHom : sInfHom (α × α) α).continuous⟩
#align lower_topology.to_has_continuous_inf Topology.IsLower.continuousInf

end CompleteLattice_Topology.IsLower

section CompleteLattice_Topology.IsUpper

variable [CompleteLattice α] [CompleteLattice β] [TopologicalSpace α] [Topology.IsUpper α]
  [TopologicalSpace β] [Topology.IsUpper β]

protected theorem sSupHom.continuous (f : sSupHom α β) : Continuous f :=
  sInfHom.continuous (α := αᵒᵈ) (β := βᵒᵈ) (sSupHom.dual.toFun f)

-- see Note [lower instance priority]
instance (priority := 90) Topology.IsUpper.continuousInf : ContinuousSup α :=
  ⟨(supsSupHom : sSupHom (α × α) α).continuous⟩

end CompleteLattice_Topology.IsUpper

lemma upper_dual_iff_lower [Preorder α] [TopologicalSpace α] :
    Topology.IsUpper αᵒᵈ ↔ Topology.IsLower α := by
  constructor
  · apply Topology.IsUpper.instTopology.IsLowerDual
  · apply Topology.IsLower.instTopology.IsUpperDual

lemma lower_dual_iff_upper [Preorder α] [TopologicalSpace α] :
    Topology.IsLower αᵒᵈ ↔ Topology.IsUpper α := by
  constructor
  · apply Topology.IsLower.instTopology.IsUpperDual
  · apply Topology.IsUpper.instTopology.IsLowerDual
