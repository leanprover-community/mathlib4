/-
Copyright (c) 2022 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
module

public import Mathlib.Topology.Order.LeftRight
public import Mathlib.Topology.Order.Monotone
public import Mathlib.Topology.Separation.Regular

/-!
# Left and right limits

We define the (strict) left and right limits of a function.

* `leftLim f x` is the strict left limit of `f` at `x` (using `f x` as a garbage value if `x`
  is isolated to its left).
* `rightLim f x` is the strict right limit of `f` at `x` (using `f x` as a garbage value if `x`
  is isolated to its right).

We develop a comprehensive API for monotone functions. Notably,

* `Monotone.continuousAt_iff_leftLim_eq_rightLim` states that a monotone function is continuous
  at a point if and only if its left and right limits coincide.
* `Monotone.countable_not_continuousAt` asserts that a monotone function taking values in a
  second-countable space has at most countably many discontinuity points.

We also port the API to antitone functions.

## TODO

Prove corresponding stronger results for `StrictMono` and `StrictAnti` functions.
-/

@[expose] public section


open Set Filter

open Topology

section

variable {Œ± Œ≤ : Type*} [LinearOrder Œ±] [TopologicalSpace Œ≤]

/-- Let `f : Œ± ‚Üí Œ≤` be a function from a linear order `Œ±` to a topological space `Œ≤`, and
let `a : Œ±`. The limit strictly to the left of `f` at `a`, denoted with `leftLim f a`, is defined
by using the order topology on `Œ±`. If `a` is isolated to its left or the function has no left
limit, we use `f a` instead to guarantee a good behavior in most cases. -/
noncomputable def Function.leftLim (f : Œ± ‚Üí Œ≤) (a : Œ±) : Œ≤ := by
  classical
  haveI : Nonempty Œ≤ := ‚ü®f a‚ü©
  letI : TopologicalSpace Œ± := Preorder.topology Œ±
  exact if ùìù[<] a = ‚ä• ‚à® ¬¨‚àÉ y, Tendsto f (ùìù[<] a) (ùìù y) then f a else limUnder (ùìù[<] a) f

/-- Let `f : Œ± ‚Üí Œ≤` be a function from a linear order `Œ±` to a topological space `Œ≤`, and
let `a : Œ±`. The limit strictly to the right of `f` at `a`, denoted with `rightLim f a`, is defined
by using the order topology on `Œ±`. If `a` is isolated to its right or the function has no right
limit, we use `f a` instead to guarantee a good behavior in most cases. -/
noncomputable def Function.rightLim (f : Œ± ‚Üí Œ≤) (a : Œ±) : Œ≤ :=
  @Function.leftLim Œ±·µí·µà Œ≤ _ _ f a

open Function

theorem leftLim_eq_of_tendsto [hŒ± : TopologicalSpace Œ±] [h'Œ± : OrderTopology Œ±] [T2Space Œ≤]
    {f : Œ± ‚Üí Œ≤} {a : Œ±} {y : Œ≤} (h : ùìù[<] a ‚â† ‚ä•) (h' : Tendsto f (ùìù[<] a) (ùìù y)) :
    leftLim f a = y := by
  have h'' : ‚àÉ y, Tendsto f (ùìù[<] a) (ùìù y) := ‚ü®y, h'‚ü©
  rw [h'Œ±.topology_eq_generate_intervals] at h h' h''
  simp only [leftLim, h, h'', not_true, or_self_iff, if_false]
  haveI := neBot_iff.2 h
  exact lim_eq h'

theorem rightLim_eq_of_tendsto [TopologicalSpace Œ±] [OrderTopology Œ±] [T2Space Œ≤]
    {f : Œ± ‚Üí Œ≤} {a : Œ±} {y : Œ≤} (h : ùìù[>] a ‚â† ‚ä•) (h' : Tendsto f (ùìù[>] a) (ùìù y)) :
    Function.rightLim f a = y :=
  leftLim_eq_of_tendsto (Œ± := Œ±·µí·µà) h h'

theorem leftLim_eq_of_eq_bot [hŒ± : TopologicalSpace Œ±] [h'Œ± : OrderTopology Œ±] (f : Œ± ‚Üí Œ≤) {a : Œ±}
    (h : ùìù[<] a = ‚ä•) : leftLim f a = f a := by
  rw [h'Œ±.topology_eq_generate_intervals] at h
  simp [leftLim, h]

theorem rightLim_eq_of_eq_bot [TopologicalSpace Œ±] [OrderTopology Œ±] (f : Œ± ‚Üí Œ≤) {a : Œ±}
    (h : ùìù[>] a = ‚ä•) : rightLim f a = f a :=
  leftLim_eq_of_eq_bot (Œ± := Œ±·µí·µà) f h

theorem leftLim_eq_of_not_tendsto
    [hŒ± : TopologicalSpace Œ±] [h'Œ± : OrderTopology Œ±] (f : Œ± ‚Üí Œ≤) {a : Œ±}
    (h : ¬¨ ‚àÉ y, Tendsto f (ùìù[<] a) (ùìù y)) : leftLim f a = f a := by
  rw [h'Œ±.topology_eq_generate_intervals] at h
  simp [leftLim, h]

theorem rightLim_eq_of_not_tendsto
    [hŒ± : TopologicalSpace Œ±] [h'Œ± : OrderTopology Œ±] (f : Œ± ‚Üí Œ≤) {a : Œ±}
    (h : ¬¨ ‚àÉ y, Tendsto f (ùìù[>] a) (ùìù y)) : rightLim f a = f a :=
  leftLim_eq_of_not_tendsto (Œ± := Œ±·µí·µà) f h

theorem leftLim_eq_of_isBot {f : Œ± ‚Üí Œ≤} {a : Œ±} (ha : IsBot a) :
    leftLim f a = f a := by
  let A : TopologicalSpace Œ± := Preorder.topology Œ±
  have : OrderTopology Œ± :=  ‚ü®rfl‚ü©
  apply leftLim_eq_of_eq_bot
  have : Iio a = ‚àÖ := by simp; grind [IsBot, IsMin]
  simp [this]

theorem rightLim_eq_of_isTop {f : Œ± ‚Üí Œ≤} {a : Œ±} (ha : IsTop a) :
    rightLim f a = f a :=
  leftLim_eq_of_isBot (Œ± := Œ±·µí·µà) ha

theorem ContinuousWithinAt.leftLim_eq [TopologicalSpace Œ±] [OrderTopology Œ±] [T2Space Œ≤]
    {f : Œ± ‚Üí Œ≤} {a : Œ±} (hf : ContinuousWithinAt f (Iic a) a) : leftLim f a = f a := by
  rcases eq_or_ne (ùìù[<] a) ‚ä• with h' | h'
  ¬∑ simp [leftLim_eq_of_eq_bot f h']
  apply leftLim_eq_of_tendsto h'
  exact hf.tendsto.mono_left (nhdsWithin_mono _ Iio_subset_Iic_self)

theorem ContinuousWithinAt.rightLim_eq [TopologicalSpace Œ±] [OrderTopology Œ±] [T2Space Œ≤]
    {f : Œ± ‚Üí Œ≤} {a : Œ±} (hf : ContinuousWithinAt f (Ici a) a) : rightLim f a = f a :=
  ContinuousWithinAt.leftLim_eq (Œ± := Œ±·µí·µà) hf

theorem tendsto_leftLim_of_tendsto [TopologicalSpace Œ±] [h'Œ± : OrderTopology Œ±]
    {f : Œ± ‚Üí Œ≤} {a : Œ±} (h : ‚àÉ y, Tendsto f (ùìù[<] a) (ùìù y)) :
    Tendsto f (ùìù[<] a) (ùìù (f.leftLim a)) := by
  rcases eq_or_neBot (ùìù[<] a) with h' | h'
  ¬∑ simp [h']
  rw [h'Œ±.topology_eq_generate_intervals] at h h' ‚ä¢
  simp only [leftLim, neBot_iff.1 h', h, not_true_eq_false, or_self, ‚ÜìreduceIte]
  exact tendsto_nhds_limUnder h

theorem tendsto_rightLim_of_tendsto [TopologicalSpace Œ±] [OrderTopology Œ±]
    {f : Œ± ‚Üí Œ≤} {a : Œ±} (h : ‚àÉ y, Tendsto f (ùìù[>] a) (ùìù y)) :
    Tendsto f (ùìù[>] a) (ùìù (f.rightLim a)) :=
  tendsto_leftLim_of_tendsto (Œ± := Œ±·µí·µà) h

theorem mapClusterPt_leftLim [TopologicalSpace Œ±] [OrderTopology Œ±]
    (f : Œ± ‚Üí Œ≤) (a : Œ±) : MapClusterPt (f.leftLim a) (ùìù[‚â§] a) f := by
  have A : (ùìù (f a) ‚äì map f (ùìù[‚â§] a)).NeBot := by
    refine inf_neBot_iff.mpr (fun s hs s' hs' ‚Ü¶ ?_)
    refine ‚ü®f a, mem_of_mem_nhds hs, ?_‚ü©
    simp only [mem_map] at hs'
    apply mem_of_mem_nhdsWithin self_mem_Iic hs'
  rcases eq_or_neBot (ùìù[<] a) with h' | h'
  ¬∑ simp only [MapClusterPt, ClusterPt, h', leftLim_eq_of_eq_bot, A]
  by_cases! H : ¬¨ ‚àÉ y, Tendsto f (ùìù[<] a) (ùìù y)
  ¬∑ simp [MapClusterPt, ClusterPt, H, leftLim_eq_of_not_tendsto, A]
  have : MapClusterPt (f.leftLim a) (ùìù[<] a) f := (tendsto_leftLim_of_tendsto H).mapClusterPt
  exact MapClusterPt.mono this (nhdsWithin_mono _ Iio_subset_Iic_self)

theorem mapClusterPt_rightLim [TopologicalSpace Œ±] [OrderTopology Œ±]
    (f : Œ± ‚Üí Œ≤) (a : Œ±) : MapClusterPt (f.rightLim a) (ùìù[‚â•] a) f :=
  mapClusterPt_leftLim (Œ± := Œ±·µí·µà) _ _

theorem continuousWithinAt_leftLim_Iic [TopologicalSpace Œ±] [OrderTopology Œ±] [T3Space Œ≤]
    {f : Œ± ‚Üí Œ≤} {a : Œ±} (h : Tendsto f (ùìù[<] a) (ùìù (f.leftLim a))) :
    ContinuousWithinAt f.leftLim (Iic a) a := by
  have : ùìù[‚â§] a = ùìù[<] a ‚äî pure a := by
    rw [‚Üê Iio_union_Icc_eq_Iic le_rfl, nhdsWithin_union]
    simp
  rw [ContinuousWithinAt, this, tendsto_sup]
  simp only [tendsto_pure_nhds, and_true]
  apply (closed_nhds_basis (f.leftLim a)).tendsto_right_iff.2
  rintro s ‚ü®s_mem, s_closed‚ü©
  rcases eq_or_neBot (ùìù[<] a) with h' | h'
  ¬∑ simp [h']
  obtain ‚ü®b, hb‚ü© : (Iio a).Nonempty := Filter.nonempty_of_mem (self_mem_nhdsWithin (a := a))
  obtain ‚ü®u, au, hu‚ü© :  ‚àÉ u, u < a ‚àß Ioo u a ‚äÜ {x | f x ‚àà s} := by
    have := (closed_nhds_basis (f.leftLim a)).tendsto_right_iff.1 h s ‚ü®s_mem, s_closed‚ü©
    simpa using (mem_nhdsLT_iff_exists_Ioo_subset' hb).1 this
  filter_upwards [Ioo_mem_nhdsLT au] with c hc
  rcases eq_or_neBot (ùìù[<] c) with h'c | h'c
  ¬∑ simpa [h'c, leftLim_eq_of_eq_bot] using hu hc
  by_cases! h''c : ¬¨ ‚àÉ y, Tendsto f (ùìù[<] c) (ùìù y)
  ¬∑ simpa [leftLim_eq_of_not_tendsto _ h''c] using hu hc
  apply s_closed.mem_of_tendsto (tendsto_leftLim_of_tendsto h''c)
  filter_upwards [Ioo_mem_nhdsLT_of_mem ‚ü®hc.1, hc.2.le‚ü©] with d hd using hu hd

theorem leftLim_leftLim [TopologicalSpace Œ±] [OrderTopology Œ±] [T3Space Œ≤]
    {f : Œ± ‚Üí Œ≤} {a : Œ±} (h : Tendsto f (ùìù[<] a) (ùìù (f.leftLim a))) :
    f.leftLim.leftLim a = f.leftLim a :=
  (continuousWithinAt_leftLim_Iic h).leftLim_eq

theorem continuousWithinAt_rightLim_Ici [TopologicalSpace Œ±] [OrderTopology Œ±] [T3Space Œ≤]
    {f : Œ± ‚Üí Œ≤} {a : Œ±} (h : Tendsto f (ùìù[>] a) (ùìù (f.rightLim a))) :
    ContinuousWithinAt f.rightLim (Ici a) a :=
  continuousWithinAt_leftLim_Iic (Œ± := Œ±·µí·µà) h

theorem rightLim_rightLim [TopologicalSpace Œ±] [OrderTopology Œ±] [T3Space Œ≤]
    {f : Œ± ‚Üí Œ≤} {a : Œ±} (h : Tendsto f (ùìù[>] a) (ùìù (f.rightLim a))) :
    f.rightLim.rightLim a = f.rightLim a :=
  leftLim_leftLim (Œ± := Œ±·µí·µà) h

theorem leftLim_rightLim [TopologicalSpace Œ±] [OrderTopology Œ±] [T3Space Œ≤]
    {f : Œ± ‚Üí Œ≤} {a : Œ±} (h : Tendsto f (ùìù[<] a) (ùìù (f.leftLim a))) [h' : (ùìù[<] a).NeBot] :
    f.rightLim.leftLim a = f.leftLim a := by
  obtain ‚ü®b, hb‚ü© : (Iio a).Nonempty := Filter.nonempty_of_mem (self_mem_nhdsWithin (a := a))
  apply leftLim_eq_of_tendsto (neBot_iff.mp h')
  apply (closed_nhds_basis (f.leftLim a)).tendsto_right_iff.2
  rintro s ‚ü®s_mem, s_closed‚ü©
  obtain ‚ü®u, au, hu‚ü© :  ‚àÉ u, u < a ‚àß Ioo u a ‚äÜ {x | f x ‚àà s} := by
    have := (closed_nhds_basis (f.leftLim a)).tendsto_right_iff.1 h s ‚ü®s_mem, s_closed‚ü©
    simpa using (mem_nhdsLT_iff_exists_Ioo_subset' hb).1 this
  filter_upwards [Ioo_mem_nhdsLT au] with c hc
  rcases eq_or_neBot (ùìù[>] c) with h'c | h'c
  ¬∑ simpa [h'c, rightLim_eq_of_eq_bot] using hu hc
  by_cases! h''c : ¬¨ ‚àÉ y, Tendsto f (ùìù[>] c) (ùìù y)
  ¬∑ simpa [rightLim_eq_of_not_tendsto _ h''c] using hu hc
  apply s_closed.mem_of_tendsto (tendsto_rightLim_of_tendsto h''c)
  filter_upwards [Ioo_mem_nhdsGT_of_mem ‚ü®hc.1.le, hc.2‚ü©] with d hd using hu hd

theorem rightLim_leftLim [TopologicalSpace Œ±] [OrderTopology Œ±] [T3Space Œ≤]
    {f : Œ± ‚Üí Œ≤} {a : Œ±} (h : Tendsto f (ùìù[>] a) (ùìù (f.rightLim a))) [h' : (ùìù[>] a).NeBot] :
    f.leftLim.rightLim a = f.rightLim a :=
  leftLim_rightLim (Œ± := Œ±·µí·µà) h (h' := h')

theorem tendsto_atTop_of_mapClusterPt
    [TopologicalSpace Œ±] [OrderTopology Œ±] [T3Space Œ≤] [NoTopOrder Œ±] {f g : Œ± ‚Üí Œ≤} {b : Œ≤}
    (h : Tendsto f atTop (ùìù b)) (h' : ‚àÄ·∂† x in atTop, MapClusterPt (g x) (ùìù x) f) :
    Tendsto g atTop (ùìù b) := by
  rcases isEmpty_or_nonempty Œ± with hŒ± | hŒ±
  ¬∑ simp [filter_eq_bot_of_isEmpty atTop]
  apply (closed_nhds_basis b).tendsto_right_iff.2
  rintro s ‚ü®s_mem, s_closed‚ü©
  obtain ‚ü®u, hu‚ü© : ‚àÉ a, ‚àÄ (b : Œ±), a ‚â§ b ‚Üí MapClusterPt (g b) (ùìù b) f ‚àß f b ‚àà s := by
    simpa [eventually_atTop] using h'.and (h s_mem)
  filter_upwards [Ioi_mem_atTop u] with a (ha : u < a)
  apply s_closed.mem_of_mapClusterPt (hu a ha.le).1
  filter_upwards [Ici_mem_nhds ha] with y hy using (hu y hy).2

theorem tendsto_atBot_of_mapClusterPt
    [TopologicalSpace Œ±] [OrderTopology Œ±] [T3Space Œ≤] [NoBotOrder Œ±] {f g : Œ± ‚Üí Œ≤} {b : Œ≤}
    (h : Tendsto f atBot (ùìù b)) (h' : ‚àÄ·∂† x in atBot, MapClusterPt (g x) (ùìù x) f) :
    Tendsto g atBot (ùìù b) :=
  tendsto_atTop_of_mapClusterPt (Œ± := Œ±·µí·µà) h h'

theorem tendsto_leftLim_atTop_of_tendsto
    [TopologicalSpace Œ±] [OrderTopology Œ±] [NoTopOrder Œ±] [T3Space Œ≤]
    {f : Œ± ‚Üí Œ≤} {b : Œ≤} (h : Tendsto f atTop (ùìù b)) :
    Tendsto f.leftLim atTop (ùìù b) := by
  apply tendsto_atTop_of_mapClusterPt h (Eventually.of_forall (fun x ‚Ü¶ ?_))
  exact MapClusterPt.mono (mapClusterPt_leftLim _ _) nhdsWithin_le_nhds

theorem tendsto_rightLim_atTop_of_tendsto [TopologicalSpace Œ±] [OrderTopology Œ±] [T3Space Œ≤]
    {f : Œ± ‚Üí Œ≤} {b : Œ≤} (h : Tendsto f atTop (ùìù b)) :
    Tendsto f.rightLim atTop (ùìù b) := by
  cases topOrderOrNoTopOrder Œ±
  ¬∑ simp only [OrderTop.atTop_eq Œ±] at h ‚ä¢
    have : f.rightLim ‚ä§ = f ‚ä§ := rightLim_eq_of_isTop isTop_top
    rw [tendsto_nhds_unique h (tendsto_pure_nhds f ‚ä§), ‚Üê this]
    apply tendsto_pure_nhds
  ¬∑ apply tendsto_atTop_of_mapClusterPt h (Eventually.of_forall (fun x ‚Ü¶ ?_))
    exact MapClusterPt.mono (mapClusterPt_rightLim _ _) nhdsWithin_le_nhds

theorem tendsto_rightLim_atBot_of_tendsto
    [TopologicalSpace Œ±] [OrderTopology Œ±] [NoBotOrder Œ±] [T3Space Œ≤]
    {f : Œ± ‚Üí Œ≤} {b : Œ≤} (h : Tendsto f atBot (ùìù b)) :
    Tendsto f.rightLim atBot (ùìù b) :=
  tendsto_leftLim_atTop_of_tendsto (Œ± := Œ±·µí·µà) h

theorem tendsto_leftLim_atBot_of_tendsto [TopologicalSpace Œ±] [OrderTopology Œ±] [T3Space Œ≤]
    {f : Œ± ‚Üí Œ≤} {b : Œ≤} (h : Tendsto f atBot (ùìù b)) :
    Tendsto f.leftLim atBot (ùìù b) :=
  tendsto_rightLim_atTop_of_tendsto (Œ± := Œ±·µí·µà) h

end

open Function

namespace Monotone

variable {Œ± Œ≤ : Type*} [LinearOrder Œ±] [ConditionallyCompleteLinearOrder Œ≤] [TopologicalSpace Œ≤]
  [OrderTopology Œ≤] {f : Œ± ‚Üí Œ≤} (hf : Monotone f) {x y : Œ±}
include hf

theorem leftLim_eq_sSup [TopologicalSpace Œ±] [OrderTopology Œ±] (h : ùìù[<] x ‚â† ‚ä•) :
    leftLim f x = sSup (f '' Iio x) :=
  leftLim_eq_of_tendsto h (hf.tendsto_nhdsLT x)

theorem rightLim_eq_sInf [TopologicalSpace Œ±] [OrderTopology Œ±] (h : ùìù[>] x ‚â† ‚ä•) :
    rightLim f x = sInf (f '' Ioi x) :=
  rightLim_eq_of_tendsto h (hf.tendsto_nhdsGT x)

theorem leftLim_le (h : x ‚â§ y) : leftLim f x ‚â§ f y := by
  letI : TopologicalSpace Œ± := Preorder.topology Œ±
  haveI : OrderTopology Œ± := ‚ü®rfl‚ü©
  rcases eq_or_ne (ùìù[<] x) ‚ä• with (h' | h')
  ¬∑ simpa [leftLim, h'] using hf h
  haveI A : NeBot (ùìù[<] x) := neBot_iff.2 h'
  rw [leftLim_eq_sSup hf h']
  refine csSup_le ?_ ?_
  ¬∑ simp only [image_nonempty]
    exact (forall_mem_nonempty_iff_neBot.2 A) _ self_mem_nhdsWithin
  ¬∑ simp only [mem_image, mem_Iio, forall_exists_index, and_imp, forall_apply_eq_imp_iff‚ÇÇ]
    intro z hz
    exact hf (hz.le.trans h)

theorem le_leftLim (h : x < y) : f x ‚â§ leftLim f y := by
  letI : TopologicalSpace Œ± := Preorder.topology Œ±
  haveI : OrderTopology Œ± := ‚ü®rfl‚ü©
  rcases eq_or_ne (ùìù[<] y) ‚ä• with (h' | h')
  ¬∑ rw [leftLim_eq_of_eq_bot _ h']
    exact hf h.le
  rw [leftLim_eq_sSup hf h']
  refine le_csSup ‚ü®f y, ?_‚ü© (mem_image_of_mem _ h)
  simp only [upperBounds, mem_image, mem_Iio, forall_exists_index, and_imp,
    forall_apply_eq_imp_iff‚ÇÇ, mem_setOf_eq]
  intro z hz
  exact hf hz.le

@[mono]
protected theorem leftLim : Monotone (leftLim f) := by
  intro x y h
  rcases eq_or_lt_of_le h with (rfl | hxy)
  ¬∑ exact le_rfl
  ¬∑ exact (hf.leftLim_le le_rfl).trans (hf.le_leftLim hxy)

theorem le_rightLim (h : x ‚â§ y) : f x ‚â§ rightLim f y :=
  hf.dual.leftLim_le h

theorem rightLim_le (h : x < y) : rightLim f x ‚â§ f y :=
  hf.dual.le_leftLim h

@[mono]
protected theorem rightLim : Monotone (rightLim f) := fun _ _ h => hf.dual.leftLim h

theorem leftLim_le_rightLim (h : x ‚â§ y) : leftLim f x ‚â§ rightLim f y :=
  (hf.leftLim_le le_rfl).trans (hf.le_rightLim h)

theorem rightLim_le_leftLim (h : x < y) : rightLim f x ‚â§ leftLim f y := by
  letI : TopologicalSpace Œ± := Preorder.topology Œ±
  haveI : OrderTopology Œ± := ‚ü®rfl‚ü©
  rcases eq_or_neBot (ùìù[<] y) with (h' | h')
  ¬∑ simpa [leftLim, h'] using rightLim_le hf h
  obtain ‚ü®a, ‚ü®xa, ay‚ü©‚ü© : (Ioo x y).Nonempty := nonempty_of_mem (Ioo_mem_nhdsLT h)
  calc
    rightLim f x ‚â§ f a := hf.rightLim_le xa
    _ ‚â§ leftLim f y := hf.le_leftLim ay

variable [TopologicalSpace Œ±] [OrderTopology Œ±]

theorem tendsto_leftLim (x : Œ±) : Tendsto f (ùìù[<] x) (ùìù (leftLim f x)) :=
  tendsto_leftLim_of_tendsto ‚ü®_, hf.tendsto_nhdsLT x‚ü©

theorem tendsto_leftLim_within (x : Œ±) : Tendsto f (ùìù[<] x) (ùìù[‚â§] leftLim f x) := by
  apply tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within f (hf.tendsto_leftLim x)
  filter_upwards [@self_mem_nhdsWithin _ _ x (Iio x)] with y hy using hf.le_leftLim hy

theorem tendsto_rightLim (x : Œ±) : Tendsto f (ùìù[>] x) (ùìù (rightLim f x)) :=
  hf.dual.tendsto_leftLim x

theorem tendsto_rightLim_within (x : Œ±) : Tendsto f (ùìù[>] x) (ùìù[‚â•] rightLim f x) :=
  hf.dual.tendsto_leftLim_within x

/-- A monotone function is continuous to the left at a point if and only if its left limit
coincides with the value of the function. -/
theorem continuousWithinAt_Iio_iff_leftLim_eq :
    ContinuousWithinAt f (Iio x) x ‚Üî leftLim f x = f x := by
  rcases eq_or_ne (ùìù[<] x) ‚ä• with (h' | h')
  ¬∑ simp [leftLim_eq_of_eq_bot f h', ContinuousWithinAt, h']
  haveI : (ùìù[Iio x] x).NeBot := neBot_iff.2 h'
  refine ‚ü®fun h => tendsto_nhds_unique (hf.tendsto_leftLim x) h.tendsto, fun h => ?_‚ü©
  have := hf.tendsto_leftLim x
  rwa [h] at this

/-- A monotone function is continuous to the right at a point if and only if its right limit
coincides with the value of the function. -/
theorem continuousWithinAt_Ioi_iff_rightLim_eq :
    ContinuousWithinAt f (Ioi x) x ‚Üî rightLim f x = f x :=
  hf.dual.continuousWithinAt_Iio_iff_leftLim_eq

/-- A monotone function is continuous at a point if and only if its left and right limits
coincide. -/
theorem continuousAt_iff_leftLim_eq_rightLim : ContinuousAt f x ‚Üî leftLim f x = rightLim f x := by
  refine ‚ü®fun h => ?_, fun h => ?_‚ü©
  ¬∑ have A : leftLim f x = f x :=
      hf.continuousWithinAt_Iio_iff_leftLim_eq.1 h.continuousWithinAt
    have B : rightLim f x = f x :=
      hf.continuousWithinAt_Ioi_iff_rightLim_eq.1 h.continuousWithinAt
    exact A.trans B.symm
  ¬∑ have h' : leftLim f x = f x := by
      apply le_antisymm (leftLim_le hf (le_refl _))
      rw [h]
      exact le_rightLim hf (le_refl _)
    refine continuousAt_iff_continuous_left'_right'.2 ‚ü®?_, ?_‚ü©
    ¬∑ exact hf.continuousWithinAt_Iio_iff_leftLim_eq.2 h'
    ¬∑ rw [h] at h'
      exact hf.continuousWithinAt_Ioi_iff_rightLim_eq.2 h'

end Monotone

namespace Antitone

variable {Œ± Œ≤ : Type*} [LinearOrder Œ±] [ConditionallyCompleteLinearOrder Œ≤] [TopologicalSpace Œ≤]
  [OrderTopology Œ≤] {f : Œ± ‚Üí Œ≤} (hf : Antitone f) {x y : Œ±}
include hf

theorem le_leftLim (h : x ‚â§ y) : f y ‚â§ leftLim f x :=
  hf.dual_right.leftLim_le h

theorem leftLim_le (h : x < y) : leftLim f y ‚â§ f x :=
  hf.dual_right.le_leftLim h

@[mono]
protected theorem leftLim : Antitone (leftLim f) :=
  hf.dual_right.leftLim

theorem rightLim_le (h : x ‚â§ y) : rightLim f y ‚â§ f x :=
  hf.dual_right.le_rightLim h

theorem le_rightLim (h : x < y) : f y ‚â§ rightLim f x :=
  hf.dual_right.rightLim_le h

@[mono]
protected theorem rightLim : Antitone (rightLim f) :=
  hf.dual_right.rightLim

theorem rightLim_le_leftLim (h : x ‚â§ y) : rightLim f y ‚â§ leftLim f x :=
  hf.dual_right.leftLim_le_rightLim h

theorem leftLim_le_rightLim (h : x < y) : leftLim f y ‚â§ rightLim f x :=
  hf.dual_right.rightLim_le_leftLim h

variable [TopologicalSpace Œ±] [OrderTopology Œ±]

theorem tendsto_leftLim (x : Œ±) : Tendsto f (ùìù[<] x) (ùìù (leftLim f x)) :=
  hf.dual_right.tendsto_leftLim x

theorem tendsto_leftLim_within (x : Œ±) : Tendsto f (ùìù[<] x) (ùìù[‚â•] leftLim f x) :=
  hf.dual_right.tendsto_leftLim_within x

theorem tendsto_rightLim (x : Œ±) : Tendsto f (ùìù[>] x) (ùìù (rightLim f x)) :=
  hf.dual_right.tendsto_rightLim x

theorem tendsto_rightLim_within (x : Œ±) : Tendsto f (ùìù[>] x) (ùìù[‚â§] rightLim f x) :=
  hf.dual_right.tendsto_rightLim_within x

/-- An antitone function is continuous to the left at a point if and only if its left limit
coincides with the value of the function. -/
theorem continuousWithinAt_Iio_iff_leftLim_eq :
    ContinuousWithinAt f (Iio x) x ‚Üî leftLim f x = f x :=
  hf.dual_right.continuousWithinAt_Iio_iff_leftLim_eq

/-- An antitone function is continuous to the right at a point if and only if its right limit
coincides with the value of the function. -/
theorem continuousWithinAt_Ioi_iff_rightLim_eq :
    ContinuousWithinAt f (Ioi x) x ‚Üî rightLim f x = f x :=
  hf.dual_right.continuousWithinAt_Ioi_iff_rightLim_eq

/-- An antitone function is continuous at a point if and only if its left and right limits
coincide. -/
theorem continuousAt_iff_leftLim_eq_rightLim : ContinuousAt f x ‚Üî leftLim f x = rightLim f x :=
  hf.dual_right.continuousAt_iff_leftLim_eq_rightLim

end Antitone
