/-
Copyright (c) 2022 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Topology.Order.LeftRight
import Mathlib.Topology.Order.Monotone

#align_import topology.algebra.order.left_right_lim from "leanprover-community/mathlib"@"0a0ec35061ed9960bf0e7ffb0335f44447b58977"

/-!
# Left and right limits

We define the (strict) left and right limits of a function.

* `leftLim f x` is the strict left limit of `f` at `x` (using `f x` as a garbage value if `x`
  is isolated to its left).
* `rightLim f x` is the strict right limit of `f` at `x` (using `f x` as a garbage value if `x`
  is isolated to its right).

We develop a comprehensive API for monotone functions. Notably,

* `Monotone.continuousAt_iff_leftLim_eq_rightLim` states that a monotone function is continuous
  at a point if and only if its left and right limits coincide.
* `Monotone.countable_not_continuousAt` asserts that a monotone function taking values in a
  second-countable space has at most countably many discontinuity points.

We also port the API to antitone functions.

## TODO

Prove corresponding stronger results for `StrictMono` and `StrictAnti` functions.
-/


open Set Filter

open Topology

section

variable {Œ± Œ≤ : Type*} [LinearOrder Œ±] [TopologicalSpace Œ≤]

/-- Let `f : Œ± ‚Üí Œ≤` be a function from a linear order `Œ±` to a topological space `Œ≤`, and
let `a : Œ±`. The limit strictly to the left of `f` at `a`, denoted with `leftLim f a`, is defined
by using the order topology on `Œ±`. If `a` is isolated to its left or the function has no left
limit, we use `f a` instead to guarantee a good behavior in most cases. -/
noncomputable def Function.leftLim (f : Œ± ‚Üí Œ≤) (a : Œ±) : Œ≤ := by
  classical
  haveI : Nonempty Œ≤ := ‚ü®f a‚ü©
  letI : TopologicalSpace Œ± := Preorder.topology Œ±
  exact if ùìù[<] a = ‚ä• ‚à® ¬¨‚àÉ y, Tendsto f (ùìù[<] a) (ùìù y) then f a else limUnder (ùìù[<] a) f
#align function.left_lim Function.leftLim

/-- Let `f : Œ± ‚Üí Œ≤` be a function from a linear order `Œ±` to a topological space `Œ≤`, and
let `a : Œ±`. The limit strictly to the right of `f` at `a`, denoted with `rightLim f a`, is defined
by using the order topology on `Œ±`. If `a` is isolated to its right or the function has no right
limit, , we use `f a` instead to guarantee a good behavior in most cases. -/
noncomputable def Function.rightLim (f : Œ± ‚Üí Œ≤) (a : Œ±) : Œ≤ :=
  @Function.leftLim Œ±·µí·µà Œ≤ _ _ f a
#align function.right_lim Function.rightLim

open Function

theorem leftLim_eq_of_tendsto [hŒ± : TopologicalSpace Œ±] [h'Œ± : OrderTopology Œ±] [T2Space Œ≤]
    {f : Œ± ‚Üí Œ≤} {a : Œ±} {y : Œ≤} (h : ùìù[<] a ‚â† ‚ä•) (h' : Tendsto f (ùìù[<] a) (ùìù y)) :
    leftLim f a = y := by
  have h'' : ‚àÉ y, Tendsto f (ùìù[<] a) (ùìù y) := ‚ü®y, h'‚ü©
  rw [h'Œ±.topology_eq_generate_intervals] at h h' h''
  simp only [leftLim, h, h'', not_true, or_self_iff, if_false]
  haveI := neBot_iff.2 h
  exact lim_eq h'
#align left_lim_eq_of_tendsto leftLim_eq_of_tendsto

theorem leftLim_eq_of_eq_bot [hŒ± : TopologicalSpace Œ±] [h'Œ± : OrderTopology Œ±] (f : Œ± ‚Üí Œ≤) {a : Œ±}
    (h : ùìù[<] a = ‚ä•) : leftLim f a = f a := by
  rw [h'Œ±.topology_eq_generate_intervals] at h
  simp [leftLim, ite_eq_left_iff, h]
#align left_lim_eq_of_eq_bot leftLim_eq_of_eq_bot

theorem rightLim_eq_of_tendsto [TopologicalSpace Œ±] [OrderTopology Œ±] [T2Space Œ≤]
    {f : Œ± ‚Üí Œ≤} {a : Œ±} {y : Œ≤} (h : ùìù[>] a ‚â† ‚ä•) (h' : Tendsto f (ùìù[>] a) (ùìù y)) :
    Function.rightLim f a = y :=
  @leftLim_eq_of_tendsto Œ±·µí·µà _ _ _ _ _ _ f a y h h'
#align right_lim_eq_of_tendsto rightLim_eq_of_tendsto

theorem rightLim_eq_of_eq_bot [TopologicalSpace Œ±] [OrderTopology Œ±] (f : Œ± ‚Üí Œ≤) {a : Œ±}
    (h : ùìù[>] a = ‚ä•) : rightLim f a = f a :=
  @leftLim_eq_of_eq_bot Œ±·µí·µà _ _ _ _ _  f a h

end

open Function

namespace Monotone

variable {Œ± Œ≤ : Type*} [LinearOrder Œ±] [ConditionallyCompleteLinearOrder Œ≤] [TopologicalSpace Œ≤]
  [OrderTopology Œ≤] {f : Œ± ‚Üí Œ≤} (hf : Monotone f) {x y : Œ±}

theorem leftLim_eq_sSup [TopologicalSpace Œ±] [OrderTopology Œ±] (h : ùìù[<] x ‚â† ‚ä•) :
    leftLim f x = sSup (f '' Iio x) :=
  leftLim_eq_of_tendsto h (hf.tendsto_nhdsWithin_Iio x)
#align monotone.left_lim_eq_Sup Monotone.leftLim_eq_sSup

theorem rightLim_eq_sInf [TopologicalSpace Œ±] [OrderTopology Œ±] (h : ùìù[>] x ‚â† ‚ä•) :
    rightLim f x = sInf (f '' Ioi x) :=
  rightLim_eq_of_tendsto h (hf.tendsto_nhdsWithin_Ioi x)
#align right_lim_eq_Inf Monotone.rightLim_eq_sInf

theorem leftLim_le (h : x ‚â§ y) : leftLim f x ‚â§ f y := by
  letI : TopologicalSpace Œ± := Preorder.topology Œ±
  haveI : OrderTopology Œ± := ‚ü®rfl‚ü©
  rcases eq_or_ne (ùìù[<] x) ‚ä• with (h' | h')
  ¬∑ simpa [leftLim, h'] using hf h
  haveI A : NeBot (ùìù[<] x) := neBot_iff.2 h'
  rw [leftLim_eq_sSup hf h']
  refine csSup_le ?_ ?_
  ¬∑ simp only [image_nonempty]
    exact (forall_mem_nonempty_iff_neBot.2 A) _ self_mem_nhdsWithin
  ¬∑ simp only [mem_image, mem_Iio, forall_exists_index, and_imp, forall_apply_eq_imp_iff‚ÇÇ]
    intro z hz
    exact hf (hz.le.trans h)
#align monotone.left_lim_le Monotone.leftLim_le

theorem le_leftLim (h : x < y) : f x ‚â§ leftLim f y := by
  letI : TopologicalSpace Œ± := Preorder.topology Œ±
  haveI : OrderTopology Œ± := ‚ü®rfl‚ü©
  rcases eq_or_ne (ùìù[<] y) ‚ä• with (h' | h')
  ¬∑ rw [leftLim_eq_of_eq_bot _ h']
    exact hf h.le
  rw [leftLim_eq_sSup hf h']
  refine le_csSup ‚ü®f y, ?_‚ü© (mem_image_of_mem _ h)
  simp only [upperBounds, mem_image, mem_Iio, forall_exists_index, and_imp,
    forall_apply_eq_imp_iff‚ÇÇ, mem_setOf_eq]
  intro z hz
  exact hf hz.le
#align monotone.le_left_lim Monotone.le_leftLim

@[mono]
protected theorem leftLim : Monotone (leftLim f) := by
  intro x y h
  rcases eq_or_lt_of_le h with (rfl | hxy)
  ¬∑ exact le_rfl
  ¬∑ exact (hf.leftLim_le le_rfl).trans (hf.le_leftLim hxy)
#align monotone.left_lim Monotone.leftLim

theorem le_rightLim (h : x ‚â§ y) : f x ‚â§ rightLim f y :=
  hf.dual.leftLim_le h
#align monotone.le_right_lim Monotone.le_rightLim

theorem rightLim_le (h : x < y) : rightLim f x ‚â§ f y :=
  hf.dual.le_leftLim h
#align monotone.right_lim_le Monotone.rightLim_le

@[mono]
protected theorem rightLim : Monotone (rightLim f) := fun _ _ h => hf.dual.leftLim h
#align monotone.right_lim Monotone.rightLim

theorem leftLim_le_rightLim (h : x ‚â§ y) : leftLim f x ‚â§ rightLim f y :=
  (hf.leftLim_le le_rfl).trans (hf.le_rightLim h)
#align monotone.left_lim_le_right_lim Monotone.leftLim_le_rightLim

theorem rightLim_le_leftLim (h : x < y) : rightLim f x ‚â§ leftLim f y := by
  letI : TopologicalSpace Œ± := Preorder.topology Œ±
  haveI : OrderTopology Œ± := ‚ü®rfl‚ü©
  rcases eq_or_ne (ùìù[<] y) ‚ä• with (h' | h')
  ¬∑ simp [leftLim, h']
    exact rightLim_le hf h
  obtain ‚ü®a, ‚ü®xa, ay‚ü©‚ü© : (Ioo x y).Nonempty :=
    forall_mem_nonempty_iff_neBot.2 (neBot_iff.2 h') (Ioo x y)
      (Ioo_mem_nhdsWithin_Iio ‚ü®h, le_refl _‚ü©)
  calc
    rightLim f x ‚â§ f a := hf.rightLim_le xa
    _ ‚â§ leftLim f y := hf.le_leftLim ay
#align monotone.right_lim_le_left_lim Monotone.rightLim_le_leftLim

variable [TopologicalSpace Œ±] [OrderTopology Œ±]

theorem tendsto_leftLim (x : Œ±) : Tendsto f (ùìù[<] x) (ùìù (leftLim f x)) := by
  rcases eq_or_ne (ùìù[<] x) ‚ä• with (h' | h')
  ¬∑ simp [h']
  rw [leftLim_eq_sSup hf h']
  exact hf.tendsto_nhdsWithin_Iio x
#align monotone.tendsto_left_lim Monotone.tendsto_leftLim

theorem tendsto_leftLim_within (x : Œ±) : Tendsto f (ùìù[<] x) (ùìù[‚â§] leftLim f x) := by
  apply tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within f (hf.tendsto_leftLim x)
  filter_upwards [@self_mem_nhdsWithin _ _ x (Iio x)] with y hy using hf.le_leftLim hy
#align monotone.tendsto_left_lim_within Monotone.tendsto_leftLim_within

theorem tendsto_rightLim (x : Œ±) : Tendsto f (ùìù[>] x) (ùìù (rightLim f x)) :=
  hf.dual.tendsto_leftLim x
#align monotone.tendsto_right_lim Monotone.tendsto_rightLim

theorem tendsto_rightLim_within (x : Œ±) : Tendsto f (ùìù[>] x) (ùìù[‚â•] rightLim f x) :=
  hf.dual.tendsto_leftLim_within x
#align monotone.tendsto_right_lim_within Monotone.tendsto_rightLim_within

/-- A monotone function is continuous to the left at a point if and only if its left limit
coincides with the value of the function. -/
theorem continuousWithinAt_Iio_iff_leftLim_eq :
    ContinuousWithinAt f (Iio x) x ‚Üî leftLim f x = f x := by
  rcases eq_or_ne (ùìù[<] x) ‚ä• with (h' | h')
  ¬∑ simp [leftLim_eq_of_eq_bot f h', ContinuousWithinAt, h']
  haveI : (ùìù[Iio x] x).NeBot := neBot_iff.2 h'
  refine ‚ü®fun h => tendsto_nhds_unique (hf.tendsto_leftLim x) h.tendsto, fun h => ?_‚ü©
  have := hf.tendsto_leftLim x
  rwa [h] at this
#align monotone.continuous_within_at_Iio_iff_left_lim_eq Monotone.continuousWithinAt_Iio_iff_leftLim_eq

/-- A monotone function is continuous to the right at a point if and only if its right limit
coincides with the value of the function. -/
theorem continuousWithinAt_Ioi_iff_rightLim_eq :
    ContinuousWithinAt f (Ioi x) x ‚Üî rightLim f x = f x :=
  hf.dual.continuousWithinAt_Iio_iff_leftLim_eq
#align monotone.continuous_within_at_Ioi_iff_right_lim_eq Monotone.continuousWithinAt_Ioi_iff_rightLim_eq

/-- A monotone function is continuous at a point if and only if its left and right limits
coincide. -/
theorem continuousAt_iff_leftLim_eq_rightLim : ContinuousAt f x ‚Üî leftLim f x = rightLim f x := by
  refine ‚ü®fun h => ?_, fun h => ?_‚ü©
  ¬∑ have A : leftLim f x = f x :=
      hf.continuousWithinAt_Iio_iff_leftLim_eq.1 h.continuousWithinAt
    have B : rightLim f x = f x :=
      hf.continuousWithinAt_Ioi_iff_rightLim_eq.1 h.continuousWithinAt
    exact A.trans B.symm
  ¬∑ have h' : leftLim f x = f x := by
      apply le_antisymm (leftLim_le hf (le_refl _))
      rw [h]
      exact le_rightLim hf (le_refl _)
    refine continuousAt_iff_continuous_left'_right'.2 ‚ü®?_, ?_‚ü©
    ¬∑ exact hf.continuousWithinAt_Iio_iff_leftLim_eq.2 h'
    ¬∑ rw [h] at h'
      exact hf.continuousWithinAt_Ioi_iff_rightLim_eq.2 h'
#align monotone.continuous_at_iff_left_lim_eq_right_lim Monotone.continuousAt_iff_leftLim_eq_rightLim

/-- In a second countable space, the set of points where a monotone function is not right-continuous
is at most countable. Superseded by `countable_not_continuousAt` which gives the two-sided
version. -/
theorem countable_not_continuousWithinAt_Ioi [SecondCountableTopology Œ≤] :
    Set.Countable { x | ¬¨ContinuousWithinAt f (Ioi x) x } := by
  apply (countable_image_lt_image_Ioi f).mono
  rintro x (hx : ¬¨ContinuousWithinAt f (Ioi x) x)
  dsimp
  contrapose! hx
  refine tendsto_order.2 ‚ü®fun m hm => ?_, fun u hu => ?_‚ü©
  ¬∑ filter_upwards [@self_mem_nhdsWithin _ _ x (Ioi x)] with y hy using hm.trans_le
      (hf (le_of_lt hy))
  rcases hx u hu with ‚ü®v, xv, fvu‚ü©
  have : Ioo x v ‚àà ùìù[>] x := Ioo_mem_nhdsWithin_Ioi ‚ü®le_refl _, xv‚ü©
  filter_upwards [this] with y hy
  apply (hf hy.2.le).trans_lt fvu
#align monotone.countable_not_continuous_within_at_Ioi Monotone.countable_not_continuousWithinAt_Ioi

/-- In a second countable space, the set of points where a monotone function is not left-continuous
is at most countable. Superseded by `countable_not_continuousAt` which gives the two-sided
version. -/
theorem countable_not_continuousWithinAt_Iio [SecondCountableTopology Œ≤] :
    Set.Countable { x | ¬¨ContinuousWithinAt f (Iio x) x } :=
  hf.dual.countable_not_continuousWithinAt_Ioi
#align monotone.countable_not_continuous_within_at_Iio Monotone.countable_not_continuousWithinAt_Iio

/-- In a second countable space, the set of points where a monotone function is not continuous
is at most countable. -/
theorem countable_not_continuousAt [SecondCountableTopology Œ≤] :
    Set.Countable { x | ¬¨ContinuousAt f x } := by
  apply
    (hf.countable_not_continuousWithinAt_Ioi.union hf.countable_not_continuousWithinAt_Iio).mono
      _
  refine compl_subset_compl.1 ?_
  simp only [compl_union]
  rintro x ‚ü®hx, h'x‚ü©
  simp only [mem_setOf_eq, Classical.not_not, mem_compl_iff] at hx h'x ‚ä¢
  exact continuousAt_iff_continuous_left'_right'.2 ‚ü®h'x, hx‚ü©
#align monotone.countable_not_continuous_at Monotone.countable_not_continuousAt

end Monotone

namespace Antitone

variable {Œ± Œ≤ : Type*} [LinearOrder Œ±] [ConditionallyCompleteLinearOrder Œ≤] [TopologicalSpace Œ≤]
  [OrderTopology Œ≤] {f : Œ± ‚Üí Œ≤} (hf : Antitone f) {x y : Œ±}

theorem le_leftLim (h : x ‚â§ y) : f y ‚â§ leftLim f x :=
  hf.dual_right.leftLim_le h
#align antitone.le_left_lim Antitone.le_leftLim

theorem leftLim_le (h : x < y) : leftLim f y ‚â§ f x :=
  hf.dual_right.le_leftLim h
#align antitone.left_lim_le Antitone.leftLim_le

@[mono]
protected theorem leftLim : Antitone (leftLim f) :=
  hf.dual_right.leftLim
#align antitone.left_lim Antitone.leftLim

theorem rightLim_le (h : x ‚â§ y) : rightLim f y ‚â§ f x :=
  hf.dual_right.le_rightLim h
#align antitone.right_lim_le Antitone.rightLim_le

theorem le_rightLim (h : x < y) : f y ‚â§ rightLim f x :=
  hf.dual_right.rightLim_le h
#align antitone.le_right_lim Antitone.le_rightLim

@[mono]
protected theorem rightLim : Antitone (rightLim f) :=
  hf.dual_right.rightLim
#align antitone.right_lim Antitone.rightLim

theorem rightLim_le_leftLim (h : x ‚â§ y) : rightLim f y ‚â§ leftLim f x :=
  hf.dual_right.leftLim_le_rightLim h
#align antitone.right_lim_le_left_lim Antitone.rightLim_le_leftLim

theorem leftLim_le_rightLim (h : x < y) : leftLim f y ‚â§ rightLim f x :=
  hf.dual_right.rightLim_le_leftLim h
#align antitone.left_lim_le_right_lim Antitone.leftLim_le_rightLim

variable [TopologicalSpace Œ±] [OrderTopology Œ±]

theorem tendsto_leftLim (x : Œ±) : Tendsto f (ùìù[<] x) (ùìù (leftLim f x)) :=
  hf.dual_right.tendsto_leftLim x
#align antitone.tendsto_left_lim Antitone.tendsto_leftLim

theorem tendsto_leftLim_within (x : Œ±) : Tendsto f (ùìù[<] x) (ùìù[‚â•] leftLim f x) :=
  hf.dual_right.tendsto_leftLim_within x
#align antitone.tendsto_left_lim_within Antitone.tendsto_leftLim_within

theorem tendsto_rightLim (x : Œ±) : Tendsto f (ùìù[>] x) (ùìù (rightLim f x)) :=
  hf.dual_right.tendsto_rightLim x
#align antitone.tendsto_right_lim Antitone.tendsto_rightLim

theorem tendsto_rightLim_within (x : Œ±) : Tendsto f (ùìù[>] x) (ùìù[‚â§] rightLim f x) :=
  hf.dual_right.tendsto_rightLim_within x
#align antitone.tendsto_right_lim_within Antitone.tendsto_rightLim_within

/-- An antitone function is continuous to the left at a point if and only if its left limit
coincides with the value of the function. -/
theorem continuousWithinAt_Iio_iff_leftLim_eq :
    ContinuousWithinAt f (Iio x) x ‚Üî leftLim f x = f x :=
  hf.dual_right.continuousWithinAt_Iio_iff_leftLim_eq
#align antitone.continuous_within_at_Iio_iff_left_lim_eq Antitone.continuousWithinAt_Iio_iff_leftLim_eq

/-- An antitone function is continuous to the right at a point if and only if its right limit
coincides with the value of the function. -/
theorem continuousWithinAt_Ioi_iff_rightLim_eq :
    ContinuousWithinAt f (Ioi x) x ‚Üî rightLim f x = f x :=
  hf.dual_right.continuousWithinAt_Ioi_iff_rightLim_eq
#align antitone.continuous_within_at_Ioi_iff_right_lim_eq Antitone.continuousWithinAt_Ioi_iff_rightLim_eq

/-- An antitone function is continuous at a point if and only if its left and right limits
coincide. -/
theorem continuousAt_iff_leftLim_eq_rightLim : ContinuousAt f x ‚Üî leftLim f x = rightLim f x :=
  hf.dual_right.continuousAt_iff_leftLim_eq_rightLim
#align antitone.continuous_at_iff_left_lim_eq_right_lim Antitone.continuousAt_iff_leftLim_eq_rightLim

/-- In a second countable space, the set of points where an antitone function is not continuous
is at most countable. -/
theorem countable_not_continuousAt [SecondCountableTopology Œ≤] :
    Set.Countable { x | ¬¨ContinuousAt f x } :=
  hf.dual_right.countable_not_continuousAt
#align antitone.countable_not_continuous_at Antitone.countable_not_continuousAt

end Antitone
