/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro, Yury Kudryashov
-/
import Mathlib.Topology.Separation

/-!
# Order-closed topologies

In this file we introduce 3 typeclass mixins that relate topology and order structures:

- `ClosedIicTopology` says that all the intervals $(-‚àû, a]$ (formally, `Set.Iic a`)
  are closed sets;
- `ClosedIciTopoplogy` says that all the intervals $[a, +‚àû)$ (formally, `Set.Ici a`)
  are closed sets;
- `OrderClosedTopology` says that the set of points `(x, y)` such that `x ‚â§ y`
  is closed in the product topology.

The last predicate implies the first two.

We prove many basic properties of such topologies.

## Main statements

This file contains the proofs of the following facts.
For exact requirements
(`OrderClosedTopology` vs `ClosedIciTopoplogy` vs `ClosedIicTopology,
`Preorder` vs `PartialOrder` vs `LinearOrder` etc)
see their statements.

### Open / closed sets

* `isOpen_lt` : if `f` and `g` are continuous functions, then `{x | f x < g x}` is open;
* `isOpen_Iio`, `isOpen_Ioi`, `isOpen_Ioo` : open intervals are open;
* `isClosed_le` : if `f` and `g` are continuous functions, then `{x | f x ‚â§ g x}` is closed;
* `isClosed_Iic`, `isClosed_Ici`, `isClosed_Icc` : closed intervals are closed;
* `frontier_le_subset_eq`, `frontier_lt_subset_eq` : frontiers of both `{x | f x ‚â§ g x}`
  and `{x | f x < g x}` are included by `{x | f x = g x}`;

### Convergence and inequalities

* `le_of_tendsto_of_tendsto` : if `f` converges to `a`, `g` converges to `b`, and eventually
  `f x ‚â§ g x`, then `a ‚â§ b`
* `le_of_tendsto`, `ge_of_tendsto` : if `f` converges to `a` and eventually `f x ‚â§ b`
  (resp., `b ‚â§ f x`), then `a ‚â§ b` (resp., `b ‚â§ a`); we also provide primed versions
  that assume the inequalities to hold for all `x`.

### Min, max, `sSup` and `sInf`

* `Continuous.min`, `Continuous.max`: pointwise `min`/`max` of two continuous functions is
  continuous.
* `Tendsto.min`, `Tendsto.max` : if `f` tends to `a` and `g` tends to `b`, then their pointwise
  `min`/`max` tend to `min a b` and `max a b`, respectively.
-/

open Set Filter
open OrderDual (toDual)
open scoped Topology

universe u v w
variable {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w}

/-- If `Œ±` is a topological space and a preorder, `ClosedIicTopology Œ±` means that `Iic a` is
closed for all `a : Œ±`. -/
class ClosedIicTopology (Œ± : Type*) [TopologicalSpace Œ±] [Preorder Œ±] : Prop where
  /-- For any `a`, the set `(-‚àû, a]` is closed. -/
  isClosed_Iic (a : Œ±) : IsClosed (Iic a)

/-- If `Œ±` is a topological space and a preorder, `ClosedIciTopology Œ±` means that `Ici a` is
closed for all `a : Œ±`. -/
class ClosedIciTopology (Œ± : Type*) [TopologicalSpace Œ±] [Preorder Œ±] : Prop where
  /-- For any `a`, the set `[a, +‚àû)` is closed. -/
  isClosed_Ici (a : Œ±) : IsClosed (Ici a)

/-- A topology on a set which is both a topological space and a preorder is _order-closed_ if the
set of points `(x, y)` with `x ‚â§ y` is closed in the product space. We introduce this as a mixin.
This property is satisfied for the order topology on a linear order, but it can be satisfied more
generally, and suffices to derive many interesting properties relating order and topology. -/
class OrderClosedTopology (Œ± : Type*) [TopologicalSpace Œ±] [Preorder Œ±] : Prop where
  /-- The set `{ (x, y) | x ‚â§ y }` is a closed set. -/
  isClosed_le' : IsClosed { p : Œ± √ó Œ± | p.1 ‚â§ p.2 }
#align order_closed_topology OrderClosedTopology

instance [TopologicalSpace Œ±] [h : FirstCountableTopology Œ±] : FirstCountableTopology Œ±·µí·µà := h
instance [TopologicalSpace Œ±] [h : SecondCountableTopology Œ±] : SecondCountableTopology Œ±·µí·µà := h

theorem Dense.orderDual [TopologicalSpace Œ±] {s : Set Œ±} (hs : Dense s) :
    Dense (OrderDual.ofDual ‚Åª¬π' s) :=
  hs
#align dense.order_dual Dense.orderDual

section General
variable [TopologicalSpace Œ±] [Preorder Œ±] {s : Set Œ±}

protected lemma BddAbove.of_closure : BddAbove (closure s) ‚Üí BddAbove s :=
  BddAbove.mono subset_closure

protected lemma BddBelow.of_closure : BddBelow (closure s) ‚Üí BddBelow s :=
  BddBelow.mono subset_closure

end General

section ClosedIicTopology

section Preorder

variable [TopologicalSpace Œ±] [Preorder Œ±] [ClosedIicTopology Œ±] {f : Œ≤ ‚Üí Œ±} {a b : Œ±} {s : Set Œ±}

theorem isClosed_Iic : IsClosed (Iic a) :=
  ClosedIicTopology.isClosed_Iic a
#align is_closed_Iic isClosed_Iic
#align is_closed_le' isClosed_Iic

@[deprecated isClosed_Iic (since := "2024-02-15")]
lemma ClosedIicTopology.isClosed_le' (a : Œ±) : IsClosed {x | x ‚â§ a} := isClosed_Iic a
export ClosedIicTopology (isClosed_le')

instance : ClosedIciTopology Œ±·µí·µà where
  isClosed_Ici _ := isClosed_Iic (Œ± := Œ±)

@[simp]
theorem closure_Iic (a : Œ±) : closure (Iic a) = Iic a :=
  isClosed_Iic.closure_eq
#align closure_Iic closure_Iic

theorem le_of_tendsto_of_frequently {x : Filter Œ≤} (lim : Tendsto f x (ùìù a))
    (h : ‚àÉ·∂† c in x, f c ‚â§ b) : a ‚â§ b :=
  isClosed_Iic.mem_of_frequently_of_tendsto h lim

theorem le_of_tendsto {x : Filter Œ≤} [NeBot x] (lim : Tendsto f x (ùìù a))
    (h : ‚àÄ·∂† c in x, f c ‚â§ b) : a ‚â§ b :=
  isClosed_Iic.mem_of_tendsto lim h
#align le_of_tendsto le_of_tendsto

theorem le_of_tendsto' {x : Filter Œ≤} [NeBot x] (lim : Tendsto f x (ùìù a))
    (h : ‚àÄ c, f c ‚â§ b) : a ‚â§ b :=
  le_of_tendsto lim (eventually_of_forall h)
#align le_of_tendsto' le_of_tendsto'

@[simp] lemma upperBounds_closure (s : Set Œ±) : upperBounds (closure s : Set Œ±) = upperBounds s :=
  ext fun a ‚Ü¶ by simp_rw [mem_upperBounds_iff_subset_Iic, isClosed_Iic.closure_subset_iff]
#align upper_bounds_closure upperBounds_closure

@[simp] lemma bddAbove_closure : BddAbove (closure s) ‚Üî BddAbove s := by
  simp_rw [BddAbove, upperBounds_closure]
#align bdd_above_closure bddAbove_closure

protected alias ‚ü®_, BddAbove.closure‚ü© := bddAbove_closure
#align bdd_above.of_closure BddAbove.of_closure
#align bdd_above.closure BddAbove.closure

@[simp]
theorem disjoint_nhds_atBot_iff : Disjoint (ùìù a) atBot ‚Üî ¬¨IsBot a := by
  constructor
  ¬∑ intro hd hbot
    rw [hbot.atBot_eq, disjoint_principal_right] at hd
    exact mem_of_mem_nhds hd le_rfl
  ¬∑ simp only [IsBot, not_forall]
    rintro ‚ü®b, hb‚ü©
    refine disjoint_of_disjoint_of_mem disjoint_compl_left ?_ (Iic_mem_atBot b)
    exact isClosed_Iic.isOpen_compl.mem_nhds hb

end Preorder

section NoBotOrder

variable [Preorder Œ±] [NoBotOrder Œ±] [TopologicalSpace Œ±] [ClosedIicTopology Œ±] {a : Œ±}
  {l : Filter Œ≤} [NeBot l] {f : Œ≤ ‚Üí Œ±}

theorem disjoint_nhds_atBot (a : Œ±) : Disjoint (ùìù a) atBot := by simp
#align disjoint_nhds_at_bot disjoint_nhds_atBot

@[simp]
theorem inf_nhds_atBot (a : Œ±) : ùìù a ‚äì atBot = ‚ä• := (disjoint_nhds_atBot a).eq_bot
#align inf_nhds_at_bot inf_nhds_atBot

theorem not_tendsto_nhds_of_tendsto_atBot (hf : Tendsto f l atBot) (a : Œ±) : ¬¨Tendsto f l (ùìù a) :=
  hf.not_tendsto (disjoint_nhds_atBot a).symm
#align not_tendsto_nhds_of_tendsto_at_bot not_tendsto_nhds_of_tendsto_atBot

theorem not_tendsto_atBot_of_tendsto_nhds (hf : Tendsto f l (ùìù a)) : ¬¨Tendsto f l atBot :=
  hf.not_tendsto (disjoint_nhds_atBot a)
#align not_tendsto_at_bot_of_tendsto_nhds not_tendsto_atBot_of_tendsto_nhds

end NoBotOrder

section LinearOrder

variable [TopologicalSpace Œ±] [LinearOrder Œ±] [ClosedIicTopology Œ±] [TopologicalSpace Œ≤]
  {a b c : Œ±} {f : Œ± ‚Üí Œ≤}

theorem isOpen_Ioi : IsOpen (Ioi a) := by
  rw [‚Üê compl_Iic]
  exact isClosed_Iic.isOpen_compl
#align is_open_Ioi isOpen_Ioi

@[simp]
theorem interior_Ioi : interior (Ioi a) = Ioi a :=
  isOpen_Ioi.interior_eq
#align interior_Ioi interior_Ioi

theorem Ioi_mem_nhds (h : a < b) : Ioi a ‚àà ùìù b := IsOpen.mem_nhds isOpen_Ioi h
#align Ioi_mem_nhds Ioi_mem_nhds

theorem eventually_gt_nhds (hab : b < a) : ‚àÄ·∂† x in ùìù a, b < x := Ioi_mem_nhds hab
#align eventually_gt_nhds eventually_gt_nhds

theorem Ici_mem_nhds (h : a < b) : Ici a ‚àà ùìù b :=
  mem_of_superset (Ioi_mem_nhds h) Ioi_subset_Ici_self
#align Ici_mem_nhds Ici_mem_nhds

theorem eventually_ge_nhds (hab : b < a) : ‚àÄ·∂† x in ùìù a, b ‚â§ x := Ici_mem_nhds hab
#align eventually_ge_nhds eventually_ge_nhds

theorem eventually_gt_of_tendsto_gt {l : Filter Œ≥} {f : Œ≥ ‚Üí Œ±} {u v : Œ±} (hv : u < v)
    (h : Filter.Tendsto f l (ùìù v)) : ‚àÄ·∂† a in l, u < f a :=
  h.eventually <| eventually_gt_nhds hv
#align eventually_gt_of_tendsto_gt eventually_gt_of_tendsto_gt

theorem eventually_ge_of_tendsto_gt {l : Filter Œ≥} {f : Œ≥ ‚Üí Œ±} {u v : Œ±} (hv : u < v)
    (h : Tendsto f l (ùìù v)) : ‚àÄ·∂† a in l, u ‚â§ f a :=
  h.eventually <| eventually_ge_nhds hv
#align eventually_ge_of_tendsto_gt eventually_ge_of_tendsto_gt

protected theorem Dense.exists_gt [NoMaxOrder Œ±] {s : Set Œ±} (hs : Dense s) (x : Œ±) :
    ‚àÉ y ‚àà s, x < y :=
  hs.exists_mem_open isOpen_Ioi (exists_gt x)
#align dense.exists_gt Dense.exists_gt

protected theorem Dense.exists_ge [NoMaxOrder Œ±] {s : Set Œ±} (hs : Dense s) (x : Œ±) :
    ‚àÉ y ‚àà s, x ‚â§ y :=
  (hs.exists_gt x).imp fun _ h ‚Ü¶ ‚ü®h.1, h.2.le‚ü©
#align dense.exists_ge Dense.exists_ge

theorem Dense.exists_ge' {s : Set Œ±} (hs : Dense s) (htop : ‚àÄ x, IsTop x ‚Üí x ‚àà s) (x : Œ±) :
    ‚àÉ y ‚àà s, x ‚â§ y := by
  by_cases hx : IsTop x
  ¬∑ exact ‚ü®x, htop x hx, le_rfl‚ü©
  ¬∑ simp only [IsTop, not_forall, not_le] at hx
    rcases hs.exists_mem_open isOpen_Ioi hx with ‚ü®y, hys, hy : x < y‚ü©
    exact ‚ü®y, hys, hy.le‚ü©
#align dense.exists_ge' Dense.exists_ge'

/-!
### Left neighborhoods on a `ClosedIicTopology`

Limits to the left of real functions are defined in terms of neighborhoods to the left,
either open or closed, i.e., members of `ùìù[<] a` and `ùìù[‚â§] a`.
Here we prove that all left-neighborhoods of a point are equal,
and we prove other useful characterizations which require the stronger hypothesis `OrderTopology Œ±`
in another file.
-/

/-!
#### Point excluded
-/

-- Porting note (#11215): TODO: swap `'`?
theorem Ioo_mem_nhdsWithin_Iio' (H : a < b) : Ioo a b ‚àà ùìù[<] b := by
  simpa only [‚Üê Iio_inter_Ioi] using inter_mem_nhdsWithin _ (Ioi_mem_nhds H)

theorem Ioo_mem_nhdsWithin_Iio (H : b ‚àà Ioc a c) : Ioo a c ‚àà ùìù[<] b :=
  mem_of_superset (Ioo_mem_nhdsWithin_Iio' H.1) <| Ioo_subset_Ioo_right H.2
#align Ioo_mem_nhds_within_Iio Ioo_mem_nhdsWithin_Iio

theorem CovBy.nhdsWithin_Iio (h : a ‚ãñ b) : ùìù[<] b = ‚ä• :=
  empty_mem_iff_bot.mp <| h.Ioo_eq ‚ñ∏ Ioo_mem_nhdsWithin_Iio' h.1

theorem Ico_mem_nhdsWithin_Iio (H : b ‚àà Ioc a c) : Ico a c ‚àà ùìù[<] b :=
  mem_of_superset (Ioo_mem_nhdsWithin_Iio H) Ioo_subset_Ico_self
#align Ico_mem_nhds_within_Iio Ico_mem_nhdsWithin_Iio
-- Porting note (#11215): TODO: swap `'`?
-- Porting note (#11215): TODO: swap `'`?
theorem Ico_mem_nhdsWithin_Iio' (H : a < b) : Ico a b ‚àà ùìù[<] b :=
  Ico_mem_nhdsWithin_Iio ‚ü®H, le_rfl‚ü©

theorem Ioc_mem_nhdsWithin_Iio (H : b ‚àà Ioc a c) : Ioc a c ‚àà ùìù[<] b :=
  mem_of_superset (Ioo_mem_nhdsWithin_Iio H) Ioo_subset_Ioc_self
#align Ioc_mem_nhds_within_Iio Ioc_mem_nhdsWithin_Iio

-- Porting note (#11215): TODO: swap `'`?
theorem Ioc_mem_nhdsWithin_Iio' (H : a < b) : Ioc a b ‚àà ùìù[<] b :=
  Ioc_mem_nhdsWithin_Iio ‚ü®H, le_rfl‚ü©

theorem Icc_mem_nhdsWithin_Iio (H : b ‚àà Ioc a c) : Icc a c ‚àà ùìù[<] b :=
  mem_of_superset (Ioo_mem_nhdsWithin_Iio H) Ioo_subset_Icc_self
#align Icc_mem_nhds_within_Iio Icc_mem_nhdsWithin_Iio

theorem Icc_mem_nhdsWithin_Iio' (H : a < b) : Icc a b ‚àà ùìù[<] b :=
  Icc_mem_nhdsWithin_Iio ‚ü®H, le_rfl‚ü©

@[simp]
theorem nhdsWithin_Ico_eq_nhdsWithin_Iio (h : a < b) : ùìù[Ico a b] b = ùìù[<] b :=
  nhdsWithin_inter_of_mem <| nhdsWithin_le_nhds <| Ici_mem_nhds h
#align nhds_within_Ico_eq_nhds_within_Iio nhdsWithin_Ico_eq_nhdsWithin_Iio

@[simp]
theorem nhdsWithin_Ioo_eq_nhdsWithin_Iio (h : a < b) : ùìù[Ioo a b] b = ùìù[<] b :=
  nhdsWithin_inter_of_mem <| nhdsWithin_le_nhds <| Ioi_mem_nhds h
#align nhds_within_Ioo_eq_nhds_within_Iio nhdsWithin_Ioo_eq_nhdsWithin_Iio

@[simp]
theorem continuousWithinAt_Ico_iff_Iio (h : a < b) :
    ContinuousWithinAt f (Ico a b) b ‚Üî ContinuousWithinAt f (Iio b) b := by
  simp only [ContinuousWithinAt, nhdsWithin_Ico_eq_nhdsWithin_Iio h]
#align continuous_within_at_Ico_iff_Iio continuousWithinAt_Ico_iff_Iio

@[simp]
theorem continuousWithinAt_Ioo_iff_Iio (h : a < b) :
    ContinuousWithinAt f (Ioo a b) b ‚Üî ContinuousWithinAt f (Iio b) b := by
  simp only [ContinuousWithinAt, nhdsWithin_Ioo_eq_nhdsWithin_Iio h]
#align continuous_within_at_Ioo_iff_Iio continuousWithinAt_Ioo_iff_Iio

/-!
#### Point included
-/

theorem Ioc_mem_nhdsWithin_Iic' (H : a < b) : Ioc a b ‚àà ùìù[‚â§] b :=
  inter_mem (nhdsWithin_le_nhds <| Ioi_mem_nhds H) self_mem_nhdsWithin

theorem Ioo_mem_nhdsWithin_Iic (H : b ‚àà Ioo a c) : Ioo a c ‚àà ùìù[‚â§] b :=
  mem_of_superset (Ioc_mem_nhdsWithin_Iic' H.1) <| Ioc_subset_Ioo_right H.2
#align Ioo_mem_nhds_within_Iic Ioo_mem_nhdsWithin_Iic

theorem Ico_mem_nhdsWithin_Iic (H : b ‚àà Ioo a c) : Ico a c ‚àà ùìù[‚â§] b :=
  mem_of_superset (Ioo_mem_nhdsWithin_Iic H) Ioo_subset_Ico_self
#align Ico_mem_nhds_within_Iic Ico_mem_nhdsWithin_Iic

theorem Ioc_mem_nhdsWithin_Iic (H : b ‚àà Ioc a c) : Ioc a c ‚àà ùìù[‚â§] b :=
  mem_of_superset (Ioc_mem_nhdsWithin_Iic' H.1) <| Ioc_subset_Ioc_right H.2
#align Ioc_mem_nhds_within_Iic Ioc_mem_nhdsWithin_Iic

theorem Icc_mem_nhdsWithin_Iic (H : b ‚àà Ioc a c) : Icc a c ‚àà ùìù[‚â§] b :=
  mem_of_superset (Ioc_mem_nhdsWithin_Iic H) Ioc_subset_Icc_self
#align Icc_mem_nhds_within_Iic Icc_mem_nhdsWithin_Iic

theorem Icc_mem_nhdsWithin_Iic' (H : a < b) : Icc a b ‚àà ùìù[‚â§] b :=
  Icc_mem_nhdsWithin_Iic ‚ü®H, le_rfl‚ü©

@[simp]
theorem nhdsWithin_Icc_eq_nhdsWithin_Iic (h : a < b) : ùìù[Icc a b] b = ùìù[‚â§] b :=
  nhdsWithin_inter_of_mem <| nhdsWithin_le_nhds <| Ici_mem_nhds h
#align nhds_within_Icc_eq_nhds_within_Iic nhdsWithin_Icc_eq_nhdsWithin_Iic

@[simp]
theorem nhdsWithin_Ioc_eq_nhdsWithin_Iic (h : a < b) : ùìù[Ioc a b] b = ùìù[‚â§] b :=
  nhdsWithin_inter_of_mem <| nhdsWithin_le_nhds <| Ioi_mem_nhds h
#align nhds_within_Ioc_eq_nhds_within_Iic nhdsWithin_Ioc_eq_nhdsWithin_Iic

@[simp]
theorem continuousWithinAt_Icc_iff_Iic (h : a < b) :
    ContinuousWithinAt f (Icc a b) b ‚Üî ContinuousWithinAt f (Iic b) b := by
  simp only [ContinuousWithinAt, nhdsWithin_Icc_eq_nhdsWithin_Iic h]
#align continuous_within_at_Icc_iff_Iic continuousWithinAt_Icc_iff_Iic

@[simp]
theorem continuousWithinAt_Ioc_iff_Iic (h : a < b) :
    ContinuousWithinAt f (Ioc a b) b ‚Üî ContinuousWithinAt f (Iic b) b := by
  simp only [ContinuousWithinAt, nhdsWithin_Ioc_eq_nhdsWithin_Iic h]
#align continuous_within_at_Ioc_iff_Iic continuousWithinAt_Ioc_iff_Iic

end LinearOrder

end ClosedIicTopology

section ClosedIciTopology

section Preorder

variable [TopologicalSpace Œ±] [Preorder Œ±] [ClosedIciTopology Œ±] {f : Œ≤ ‚Üí Œ±} {a b : Œ±} {s : Set Œ±}

theorem isClosed_Ici {a : Œ±} : IsClosed (Ici a) :=
  ClosedIciTopology.isClosed_Ici a
#align is_closed_Ici isClosed_Ici
#align is_closed_ge' isClosed_Ici

@[deprecated (since := "2024-02-15")]
lemma ClosedIciTopology.isClosed_ge' (a : Œ±) : IsClosed {x | a ‚â§ x} := isClosed_Ici a
export ClosedIciTopology (isClosed_ge')

instance : ClosedIicTopology Œ±·µí·µà where
  isClosed_Iic _ := isClosed_Ici (Œ± := Œ±)

@[simp]
theorem closure_Ici (a : Œ±) : closure (Ici a) = Ici a :=
  isClosed_Ici.closure_eq
#align closure_Ici closure_Ici

lemma ge_of_tendsto_of_frequently {x : Filter Œ≤} (lim : Tendsto f x (ùìù a))
    (h : ‚àÉ·∂† c in x, b ‚â§ f c) : b ‚â§ a :=
  isClosed_Ici.mem_of_frequently_of_tendsto h lim

theorem ge_of_tendsto {x : Filter Œ≤} [NeBot x] (lim : Tendsto f x (ùìù a))
    (h : ‚àÄ·∂† c in x, b ‚â§ f c) : b ‚â§ a :=
  isClosed_Ici.mem_of_tendsto lim h
#align ge_of_tendsto ge_of_tendsto

theorem ge_of_tendsto' {x : Filter Œ≤} [NeBot x] (lim : Tendsto f x (ùìù a))
    (h : ‚àÄ c, b ‚â§ f c) : b ‚â§ a :=
  ge_of_tendsto lim (eventually_of_forall h)
#align ge_of_tendsto' ge_of_tendsto'

@[simp] lemma lowerBounds_closure (s : Set Œ±) : lowerBounds (closure s : Set Œ±) = lowerBounds s :=
  ext fun a ‚Ü¶ by simp_rw [mem_lowerBounds_iff_subset_Ici, isClosed_Ici.closure_subset_iff]
#align lower_bounds_closure lowerBounds_closure

@[simp] lemma bddBelow_closure : BddBelow (closure s) ‚Üî BddBelow s := by
  simp_rw [BddBelow, lowerBounds_closure]
#align bdd_below_closure bddBelow_closure

protected alias ‚ü®_, BddBelow.closure‚ü© := bddBelow_closure
#align bdd_below.of_closure BddBelow.of_closure
#align bdd_below.closure BddBelow.closure

@[simp]
theorem disjoint_nhds_atTop_iff : Disjoint (ùìù a) atTop ‚Üî ¬¨IsTop a :=
  disjoint_nhds_atBot_iff (Œ± := Œ±·µí·µà)

end Preorder

section NoTopOrder

variable [Preorder Œ±] [NoTopOrder Œ±] [TopologicalSpace Œ±] [ClosedIciTopology Œ±] {a : Œ±}
  {l : Filter Œ≤} [NeBot l] {f : Œ≤ ‚Üí Œ±}

theorem disjoint_nhds_atTop (a : Œ±) : Disjoint (ùìù a) atTop := disjoint_nhds_atBot (toDual a)
#align disjoint_nhds_at_top disjoint_nhds_atTop

@[simp]
theorem inf_nhds_atTop (a : Œ±) : ùìù a ‚äì atTop = ‚ä• := (disjoint_nhds_atTop a).eq_bot
#align inf_nhds_at_top inf_nhds_atTop

theorem not_tendsto_nhds_of_tendsto_atTop (hf : Tendsto f l atTop) (a : Œ±) : ¬¨Tendsto f l (ùìù a) :=
  hf.not_tendsto (disjoint_nhds_atTop a).symm
#align not_tendsto_nhds_of_tendsto_at_top not_tendsto_nhds_of_tendsto_atTop

theorem not_tendsto_atTop_of_tendsto_nhds (hf : Tendsto f l (ùìù a)) : ¬¨Tendsto f l atTop :=
  hf.not_tendsto (disjoint_nhds_atTop a)
#align not_tendsto_at_top_of_tendsto_nhds not_tendsto_atTop_of_tendsto_nhds

end NoTopOrder

section LinearOrder

variable [TopologicalSpace Œ±] [LinearOrder Œ±] [ClosedIciTopology Œ±] [TopologicalSpace Œ≤]
  {a b c : Œ±} {f : Œ± ‚Üí Œ≤}

theorem isOpen_Iio : IsOpen (Iio a) := isOpen_Ioi (Œ± := Œ±·µí·µà)
#align is_open_Iio isOpen_Iio

@[simp] theorem interior_Iio : interior (Iio a) = Iio a := isOpen_Iio.interior_eq
#align interior_Iio interior_Iio

theorem Iio_mem_nhds (h : a < b) : Iio b ‚àà ùìù a := isOpen_Iio.mem_nhds h
#align Iio_mem_nhds Iio_mem_nhds

theorem eventually_lt_nhds (hab : a < b) : ‚àÄ·∂† x in ùìù a, x < b := Iio_mem_nhds hab
#align eventually_lt_nhds eventually_lt_nhds

theorem Iic_mem_nhds (h : a < b) : Iic b ‚àà ùìù a :=
  mem_of_superset (Iio_mem_nhds h) Iio_subset_Iic_self
#align Iic_mem_nhds Iic_mem_nhds

theorem eventually_le_nhds (hab : a < b) : ‚àÄ·∂† x in ùìù a, x ‚â§ b := Iic_mem_nhds hab
#align eventually_le_nhds eventually_le_nhds

theorem eventually_lt_of_tendsto_lt {l : Filter Œ≥} {f : Œ≥ ‚Üí Œ±} {u v : Œ±} (hv : v < u)
    (h : Filter.Tendsto f l (ùìù v)) : ‚àÄ·∂† a in l, f a < u :=
  h.eventually <| eventually_lt_nhds hv
#align eventually_lt_of_tendsto_lt eventually_lt_of_tendsto_lt

theorem eventually_le_of_tendsto_lt {l : Filter Œ≥} {f : Œ≥ ‚Üí Œ±} {u v : Œ±} (hv : v < u)
    (h : Tendsto f l (ùìù v)) : ‚àÄ·∂† a in l, f a ‚â§ u :=
  h.eventually <| eventually_le_nhds hv
#align eventually_le_of_tendsto_lt eventually_le_of_tendsto_lt

protected theorem Dense.exists_lt [NoMinOrder Œ±] {s : Set Œ±} (hs : Dense s) (x : Œ±) :
    ‚àÉ y ‚àà s, y < x :=
  hs.orderDual.exists_gt x
#align dense.exists_lt Dense.exists_lt

protected theorem Dense.exists_le [NoMinOrder Œ±] {s : Set Œ±} (hs : Dense s) (x : Œ±) :
    ‚àÉ y ‚àà s, y ‚â§ x :=
  hs.orderDual.exists_ge x
#align dense.exists_le Dense.exists_le

theorem Dense.exists_le' {s : Set Œ±} (hs : Dense s) (hbot : ‚àÄ x, IsBot x ‚Üí x ‚àà s) (x : Œ±) :
    ‚àÉ y ‚àà s, y ‚â§ x :=
  hs.orderDual.exists_ge' hbot x
#align dense.exists_le' Dense.exists_le'

/-!
### Right neighborhoods on a `ClosedIciTopology`

Limits to the right of real functions are defined in terms of neighborhoods to the right,
either open or closed, i.e., members of `ùìù[>] a` and `ùìù[‚â•] a`.
Here we prove that all right-neighborhoods of a point are equal,
and we prove other useful characterizations which require the stronger hypothesis `OrderTopology Œ±`
in another file.
-/

/-!
#### Point excluded
-/

theorem Ioo_mem_nhdsWithin_Ioi {a b c : Œ±} (H : b ‚àà Ico a c) : Ioo a c ‚àà ùìù[>] b :=
  mem_nhdsWithin.2
    ‚ü®Iio c, isOpen_Iio, H.2, by rw [inter_comm, Ioi_inter_Iio]; exact Ioo_subset_Ioo_left H.1‚ü©
#align Ioo_mem_nhds_within_Ioi Ioo_mem_nhdsWithin_Ioi

-- Porting note (#11215): TODO: swap `'`?
theorem Ioo_mem_nhdsWithin_Ioi' {a b : Œ±} (H : a < b) : Ioo a b ‚àà ùìù[>] a :=
  Ioo_mem_nhdsWithin_Ioi ‚ü®le_rfl, H‚ü©

theorem CovBy.nhdsWithin_Ioi {a b : Œ±} (h : a ‚ãñ b) : ùìù[>] a = ‚ä• :=
  empty_mem_iff_bot.mp <| h.Ioo_eq ‚ñ∏ Ioo_mem_nhdsWithin_Ioi' h.1

theorem Ioc_mem_nhdsWithin_Ioi {a b c : Œ±} (H : b ‚àà Ico a c) : Ioc a c ‚àà ùìù[>] b :=
  mem_of_superset (Ioo_mem_nhdsWithin_Ioi H) Ioo_subset_Ioc_self
#align Ioc_mem_nhds_within_Ioi Ioc_mem_nhdsWithin_Ioi

-- Porting note (#11215): TODO: swap `'`?
theorem Ioc_mem_nhdsWithin_Ioi' {a b : Œ±} (H : a < b) : Ioc a b ‚àà ùìù[>] a :=
  Ioc_mem_nhdsWithin_Ioi ‚ü®le_rfl, H‚ü©

theorem Ico_mem_nhdsWithin_Ioi {a b c : Œ±} (H : b ‚àà Ico a c) : Ico a c ‚àà ùìù[>] b :=
  mem_of_superset (Ioo_mem_nhdsWithin_Ioi H) Ioo_subset_Ico_self
#align Ico_mem_nhds_within_Ioi Ico_mem_nhdsWithin_Ioi

-- Porting note (#11215): TODO: swap `'`?
theorem Ico_mem_nhdsWithin_Ioi' {a b : Œ±} (H : a < b) : Ico a b ‚àà ùìù[>] a :=
  Ico_mem_nhdsWithin_Ioi ‚ü®le_rfl, H‚ü©

theorem Icc_mem_nhdsWithin_Ioi {a b c : Œ±} (H : b ‚àà Ico a c) : Icc a c ‚àà ùìù[>] b :=
  mem_of_superset (Ioo_mem_nhdsWithin_Ioi H) Ioo_subset_Icc_self
#align Icc_mem_nhds_within_Ioi Icc_mem_nhdsWithin_Ioi

-- Porting note (#11215): TODO: swap `'`?
theorem Icc_mem_nhdsWithin_Ioi' {a b : Œ±} (H : a < b) : Icc a b ‚àà ùìù[>] a :=
  Icc_mem_nhdsWithin_Ioi ‚ü®le_rfl, H‚ü©

@[simp]
theorem nhdsWithin_Ioc_eq_nhdsWithin_Ioi {a b : Œ±} (h : a < b) : ùìù[Ioc a b] a = ùìù[>] a :=
  nhdsWithin_inter_of_mem' <| nhdsWithin_le_nhds <| Iic_mem_nhds h
#align nhds_within_Ioc_eq_nhds_within_Ioi nhdsWithin_Ioc_eq_nhdsWithin_Ioi

@[simp]
theorem nhdsWithin_Ioo_eq_nhdsWithin_Ioi {a b : Œ±} (h : a < b) : ùìù[Ioo a b] a = ùìù[>] a :=
  nhdsWithin_inter_of_mem' <| nhdsWithin_le_nhds <| Iio_mem_nhds h
#align nhds_within_Ioo_eq_nhds_within_Ioi nhdsWithin_Ioo_eq_nhdsWithin_Ioi

@[simp]
theorem continuousWithinAt_Ioc_iff_Ioi (h : a < b) :
    ContinuousWithinAt f (Ioc a b) a ‚Üî ContinuousWithinAt f (Ioi a) a := by
  simp only [ContinuousWithinAt, nhdsWithin_Ioc_eq_nhdsWithin_Ioi h]
#align continuous_within_at_Ioc_iff_Ioi continuousWithinAt_Ioc_iff_Ioi

@[simp]
theorem continuousWithinAt_Ioo_iff_Ioi (h : a < b) :
    ContinuousWithinAt f (Ioo a b) a ‚Üî ContinuousWithinAt f (Ioi a) a := by
  simp only [ContinuousWithinAt, nhdsWithin_Ioo_eq_nhdsWithin_Ioi h]
#align continuous_within_at_Ioo_iff_Ioi continuousWithinAt_Ioo_iff_Ioi

/-!
### Point included
-/

theorem Ico_mem_nhdsWithin_Ici' (H : a < b) : Ico a b ‚àà ùìù[‚â•] a :=
  inter_mem_nhdsWithin _ <| Iio_mem_nhds H

theorem Ico_mem_nhdsWithin_Ici (H : b ‚àà Ico a c) : Ico a c ‚àà ùìù[‚â•] b :=
  mem_of_superset (Ico_mem_nhdsWithin_Ici' H.2) <| Ico_subset_Ico_left H.1
#align Ico_mem_nhds_within_Ici Ico_mem_nhdsWithin_Ici

theorem Ioo_mem_nhdsWithin_Ici (H : b ‚àà Ioo a c) : Ioo a c ‚àà ùìù[‚â•] b :=
  mem_of_superset (Ico_mem_nhdsWithin_Ici' H.2) <| Ico_subset_Ioo_left H.1
#align Ioo_mem_nhds_within_Ici Ioo_mem_nhdsWithin_Ici

theorem Ioc_mem_nhdsWithin_Ici (H : b ‚àà Ioo a c) : Ioc a c ‚àà ùìù[‚â•] b :=
  mem_of_superset (Ioo_mem_nhdsWithin_Ici H) Ioo_subset_Ioc_self
#align Ioc_mem_nhds_within_Ici Ioc_mem_nhdsWithin_Ici

theorem Icc_mem_nhdsWithin_Ici (H : b ‚àà Ico a c) : Icc a c ‚àà ùìù[‚â•] b :=
  mem_of_superset (Ico_mem_nhdsWithin_Ici H) Ico_subset_Icc_self
#align Icc_mem_nhds_within_Ici Icc_mem_nhdsWithin_Ici

theorem Icc_mem_nhdsWithin_Ici' (H : a < b) : Icc a b ‚àà ùìù[‚â•] a :=
  Icc_mem_nhdsWithin_Ici ‚ü®le_rfl, H‚ü©

@[simp]
theorem nhdsWithin_Icc_eq_nhdsWithin_Ici (h : a < b) : ùìù[Icc a b] a = ùìù[‚â•] a :=
  nhdsWithin_inter_of_mem' <| nhdsWithin_le_nhds <| Iic_mem_nhds h
#align nhds_within_Icc_eq_nhds_within_Ici nhdsWithin_Icc_eq_nhdsWithin_Ici

@[simp]
theorem nhdsWithin_Ico_eq_nhdsWithin_Ici (h : a < b) : ùìù[Ico a b] a = ùìù[‚â•] a :=
  nhdsWithin_inter_of_mem' <| nhdsWithin_le_nhds <| Iio_mem_nhds h
#align nhds_within_Ico_eq_nhds_within_Ici nhdsWithin_Ico_eq_nhdsWithin_Ici

@[simp]
theorem continuousWithinAt_Icc_iff_Ici (h : a < b) :
    ContinuousWithinAt f (Icc a b) a ‚Üî ContinuousWithinAt f (Ici a) a := by
  simp only [ContinuousWithinAt, nhdsWithin_Icc_eq_nhdsWithin_Ici h]
#align continuous_within_at_Icc_iff_Ici continuousWithinAt_Icc_iff_Ici

@[simp]
theorem continuousWithinAt_Ico_iff_Ici (h : a < b) :
    ContinuousWithinAt f (Ico a b) a ‚Üî ContinuousWithinAt f (Ici a) a := by
  simp only [ContinuousWithinAt, nhdsWithin_Ico_eq_nhdsWithin_Ici h]
#align continuous_within_at_Ico_iff_Ici continuousWithinAt_Ico_iff_Ici

end LinearOrder

end ClosedIciTopology

section OrderClosedTopology

section Preorder

variable [TopologicalSpace Œ±] [Preorder Œ±] [t : OrderClosedTopology Œ±]

namespace Subtype

-- todo: add `OrderEmbedding.orderClosedTopology`
instance {p : Œ± ‚Üí Prop} : OrderClosedTopology (Subtype p) :=
  have this : Continuous fun p : Subtype p √ó Subtype p => ((p.fst : Œ±), (p.snd : Œ±)) :=
    continuous_subtype_val.prod_map continuous_subtype_val
  OrderClosedTopology.mk (t.isClosed_le'.preimage this)

end Subtype

theorem isClosed_le_prod : IsClosed { p : Œ± √ó Œ± | p.1 ‚â§ p.2 } :=
  t.isClosed_le'
#align is_closed_le_prod isClosed_le_prod

theorem isClosed_le [TopologicalSpace Œ≤] {f g : Œ≤ ‚Üí Œ±} (hf : Continuous f) (hg : Continuous g) :
    IsClosed { b | f b ‚â§ g b } :=
  continuous_iff_isClosed.mp (hf.prod_mk hg) _ isClosed_le_prod
#align is_closed_le isClosed_le

instance : ClosedIicTopology Œ± where
  isClosed_Iic _ := isClosed_le continuous_id continuous_const

instance : ClosedIciTopology Œ± where
  isClosed_Ici _ := isClosed_le continuous_const continuous_id

instance : OrderClosedTopology Œ±·µí·µà :=
  ‚ü®(OrderClosedTopology.isClosed_le' (Œ± := Œ±)).preimage continuous_swap‚ü©

theorem isClosed_Icc {a b : Œ±} : IsClosed (Icc a b) :=
  IsClosed.inter isClosed_Ici isClosed_Iic
#align is_closed_Icc isClosed_Icc

@[simp]
theorem closure_Icc (a b : Œ±) : closure (Icc a b) = Icc a b :=
  isClosed_Icc.closure_eq
#align closure_Icc closure_Icc

theorem le_of_tendsto_of_tendsto {f g : Œ≤ ‚Üí Œ±} {b : Filter Œ≤} {a‚ÇÅ a‚ÇÇ : Œ±} [NeBot b]
    (hf : Tendsto f b (ùìù a‚ÇÅ)) (hg : Tendsto g b (ùìù a‚ÇÇ)) (h : f ‚â§·∂†[b] g) : a‚ÇÅ ‚â§ a‚ÇÇ :=
  have : Tendsto (fun b => (f b, g b)) b (ùìù (a‚ÇÅ, a‚ÇÇ)) := hf.prod_mk_nhds hg
  show (a‚ÇÅ, a‚ÇÇ) ‚àà { p : Œ± √ó Œ± | p.1 ‚â§ p.2 } from t.isClosed_le'.mem_of_tendsto this h
#align le_of_tendsto_of_tendsto le_of_tendsto_of_tendsto

alias tendsto_le_of_eventuallyLE := le_of_tendsto_of_tendsto
#align tendsto_le_of_eventually_le tendsto_le_of_eventuallyLE

theorem le_of_tendsto_of_tendsto' {f g : Œ≤ ‚Üí Œ±} {b : Filter Œ≤} {a‚ÇÅ a‚ÇÇ : Œ±} [NeBot b]
    (hf : Tendsto f b (ùìù a‚ÇÅ)) (hg : Tendsto g b (ùìù a‚ÇÇ)) (h : ‚àÄ x, f x ‚â§ g x) : a‚ÇÅ ‚â§ a‚ÇÇ :=
  le_of_tendsto_of_tendsto hf hg (eventually_of_forall h)
#align le_of_tendsto_of_tendsto' le_of_tendsto_of_tendsto'

@[simp]
theorem closure_le_eq [TopologicalSpace Œ≤] {f g : Œ≤ ‚Üí Œ±} (hf : Continuous f) (hg : Continuous g) :
    closure { b | f b ‚â§ g b } = { b | f b ‚â§ g b } :=
  (isClosed_le hf hg).closure_eq
#align closure_le_eq closure_le_eq

theorem closure_lt_subset_le [TopologicalSpace Œ≤] {f g : Œ≤ ‚Üí Œ±} (hf : Continuous f)
    (hg : Continuous g) : closure { b | f b < g b } ‚äÜ { b | f b ‚â§ g b } :=
  (closure_minimal fun _ => le_of_lt) <| isClosed_le hf hg
#align closure_lt_subset_le closure_lt_subset_le

theorem ContinuousWithinAt.closure_le [TopologicalSpace Œ≤] {f g : Œ≤ ‚Üí Œ±} {s : Set Œ≤} {x : Œ≤}
    (hx : x ‚àà closure s) (hf : ContinuousWithinAt f s x) (hg : ContinuousWithinAt g s x)
    (h : ‚àÄ y ‚àà s, f y ‚â§ g y) : f x ‚â§ g x :=
  show (f x, g x) ‚àà { p : Œ± √ó Œ± | p.1 ‚â§ p.2 } from
    OrderClosedTopology.isClosed_le'.closure_subset ((hf.prod hg).mem_closure hx h)
#align continuous_within_at.closure_le ContinuousWithinAt.closure_le

/-- If `s` is a closed set and two functions `f` and `g` are continuous on `s`,
then the set `{x ‚àà s | f x ‚â§ g x}` is a closed set. -/
theorem IsClosed.isClosed_le [TopologicalSpace Œ≤] {f g : Œ≤ ‚Üí Œ±} {s : Set Œ≤} (hs : IsClosed s)
    (hf : ContinuousOn f s) (hg : ContinuousOn g s) : IsClosed ({ x ‚àà s | f x ‚â§ g x }) :=
  (hf.prod hg).preimage_isClosed_of_isClosed hs OrderClosedTopology.isClosed_le'
#align is_closed.is_closed_le IsClosed.isClosed_le

theorem le_on_closure [TopologicalSpace Œ≤] {f g : Œ≤ ‚Üí Œ±} {s : Set Œ≤} (h : ‚àÄ x ‚àà s, f x ‚â§ g x)
    (hf : ContinuousOn f (closure s)) (hg : ContinuousOn g (closure s)) ‚¶Éx‚¶Ñ (hx : x ‚àà closure s) :
    f x ‚â§ g x :=
  have : s ‚äÜ { y ‚àà closure s | f y ‚â§ g y } := fun y hy => ‚ü®subset_closure hy, h y hy‚ü©
  (closure_minimal this (isClosed_closure.isClosed_le hf hg) hx).2
#align le_on_closure le_on_closure

theorem IsClosed.epigraph [TopologicalSpace Œ≤] {f : Œ≤ ‚Üí Œ±} {s : Set Œ≤} (hs : IsClosed s)
    (hf : ContinuousOn f s) : IsClosed { p : Œ≤ √ó Œ± | p.1 ‚àà s ‚àß f p.1 ‚â§ p.2 } :=
  (hs.preimage continuous_fst).isClosed_le (hf.comp continuousOn_fst Subset.rfl) continuousOn_snd
#align is_closed.epigraph IsClosed.epigraph

theorem IsClosed.hypograph [TopologicalSpace Œ≤] {f : Œ≤ ‚Üí Œ±} {s : Set Œ≤} (hs : IsClosed s)
    (hf : ContinuousOn f s) : IsClosed { p : Œ≤ √ó Œ± | p.1 ‚àà s ‚àß p.2 ‚â§ f p.1 } :=
  (hs.preimage continuous_fst).isClosed_le continuousOn_snd (hf.comp continuousOn_fst Subset.rfl)
#align is_closed.hypograph IsClosed.hypograph

end Preorder

section PartialOrder

variable [TopologicalSpace Œ±] [PartialOrder Œ±] [t : OrderClosedTopology Œ±]

-- see Note [lower instance priority]
instance (priority := 90) OrderClosedTopology.to_t2Space : T2Space Œ± :=
  t2_iff_isClosed_diagonal.2 <| by
    simpa only [diagonal, le_antisymm_iff] using
      t.isClosed_le'.inter (isClosed_le continuous_snd continuous_fst)
#align order_closed_topology.to_t2_space OrderClosedTopology.to_t2Space

end PartialOrder

section LinearOrder

variable [TopologicalSpace Œ±] [LinearOrder Œ±] [OrderClosedTopology Œ±]

theorem isOpen_lt [TopologicalSpace Œ≤] {f g : Œ≤ ‚Üí Œ±} (hf : Continuous f) (hg : Continuous g) :
    IsOpen { b | f b < g b } := by
  simpa only [lt_iff_not_le] using (isClosed_le hg hf).isOpen_compl
#align is_open_lt isOpen_lt

theorem isOpen_lt_prod : IsOpen { p : Œ± √ó Œ± | p.1 < p.2 } :=
  isOpen_lt continuous_fst continuous_snd
#align is_open_lt_prod isOpen_lt_prod

variable {a b : Œ±}

theorem isOpen_Ioo : IsOpen (Ioo a b) :=
  IsOpen.inter isOpen_Ioi isOpen_Iio
#align is_open_Ioo isOpen_Ioo

@[simp]
theorem interior_Ioo : interior (Ioo a b) = Ioo a b :=
  isOpen_Ioo.interior_eq
#align interior_Ioo interior_Ioo

theorem Ioo_subset_closure_interior : Ioo a b ‚äÜ closure (interior (Ioo a b)) := by
  simp only [interior_Ioo, subset_closure]
#align Ioo_subset_closure_interior Ioo_subset_closure_interior

theorem Ioo_mem_nhds {a b x : Œ±} (ha : a < x) (hb : x < b) : Ioo a b ‚àà ùìù x :=
  IsOpen.mem_nhds isOpen_Ioo ‚ü®ha, hb‚ü©
#align Ioo_mem_nhds Ioo_mem_nhds

theorem Ioc_mem_nhds {a b x : Œ±} (ha : a < x) (hb : x < b) : Ioc a b ‚àà ùìù x :=
  mem_of_superset (Ioo_mem_nhds ha hb) Ioo_subset_Ioc_self
#align Ioc_mem_nhds Ioc_mem_nhds

theorem Ico_mem_nhds {a b x : Œ±} (ha : a < x) (hb : x < b) : Ico a b ‚àà ùìù x :=
  mem_of_superset (Ioo_mem_nhds ha hb) Ioo_subset_Ico_self
#align Ico_mem_nhds Ico_mem_nhds

theorem Icc_mem_nhds {a b x : Œ±} (ha : a < x) (hb : x < b) : Icc a b ‚àà ùìù x :=
  mem_of_superset (Ioo_mem_nhds ha hb) Ioo_subset_Icc_self
#align Icc_mem_nhds Icc_mem_nhds

variable [TopologicalSpace Œ≥]

end LinearOrder

section LinearOrder

variable [TopologicalSpace Œ±] [LinearOrder Œ±] [OrderClosedTopology Œ±] {f g : Œ≤ ‚Üí Œ±}

section

variable [TopologicalSpace Œ≤]

theorem lt_subset_interior_le (hf : Continuous f) (hg : Continuous g) :
    { b | f b < g b } ‚äÜ interior { b | f b ‚â§ g b } :=
  (interior_maximal fun _ => le_of_lt) <| isOpen_lt hf hg
#align lt_subset_interior_le lt_subset_interior_le

theorem frontier_le_subset_eq (hf : Continuous f) (hg : Continuous g) :
    frontier { b | f b ‚â§ g b } ‚äÜ { b | f b = g b } := by
  rw [frontier_eq_closure_inter_closure, closure_le_eq hf hg]
  rintro b ‚ü®hb‚ÇÅ, hb‚ÇÇ‚ü©
  refine le_antisymm hb‚ÇÅ (closure_lt_subset_le hg hf ?_)
  convert hb‚ÇÇ using 2; simp only [not_le.symm]; rfl
#align frontier_le_subset_eq frontier_le_subset_eq

theorem frontier_Iic_subset (a : Œ±) : frontier (Iic a) ‚äÜ {a} :=
  frontier_le_subset_eq (@continuous_id Œ± _) continuous_const
#align frontier_Iic_subset frontier_Iic_subset

theorem frontier_Ici_subset (a : Œ±) : frontier (Ici a) ‚äÜ {a} :=
  frontier_Iic_subset (Œ± := Œ±·µí·µà) _
#align frontier_Ici_subset frontier_Ici_subset

theorem frontier_lt_subset_eq (hf : Continuous f) (hg : Continuous g) :
    frontier { b | f b < g b } ‚äÜ { b | f b = g b } := by
  simpa only [‚Üê not_lt, ‚Üê compl_setOf, frontier_compl, eq_comm] using frontier_le_subset_eq hg hf
#align frontier_lt_subset_eq frontier_lt_subset_eq

theorem continuous_if_le [TopologicalSpace Œ≥] [‚àÄ x, Decidable (f x ‚â§ g x)] {f' g' : Œ≤ ‚Üí Œ≥}
    (hf : Continuous f) (hg : Continuous g) (hf' : ContinuousOn f' { x | f x ‚â§ g x })
    (hg' : ContinuousOn g' { x | g x ‚â§ f x }) (hfg : ‚àÄ x, f x = g x ‚Üí f' x = g' x) :
    Continuous fun x => if f x ‚â§ g x then f' x else g' x := by
  refine continuous_if (fun a ha => hfg _ (frontier_le_subset_eq hf hg ha)) ?_ (hg'.mono ?_)
  ¬∑ rwa [(isClosed_le hf hg).closure_eq]
  ¬∑ simp only [not_le]
    exact closure_lt_subset_le hg hf
#align continuous_if_le continuous_if_le

theorem Continuous.if_le [TopologicalSpace Œ≥] [‚àÄ x, Decidable (f x ‚â§ g x)] {f' g' : Œ≤ ‚Üí Œ≥}
    (hf' : Continuous f') (hg' : Continuous g') (hf : Continuous f) (hg : Continuous g)
    (hfg : ‚àÄ x, f x = g x ‚Üí f' x = g' x) : Continuous fun x => if f x ‚â§ g x then f' x else g' x :=
  continuous_if_le hf hg hf'.continuousOn hg'.continuousOn hfg
#align continuous.if_le Continuous.if_le

theorem Filter.Tendsto.eventually_lt {l : Filter Œ≥} {f g : Œ≥ ‚Üí Œ±} {y z : Œ±} (hf : Tendsto f l (ùìù y))
    (hg : Tendsto g l (ùìù z)) (hyz : y < z) : ‚àÄ·∂† x in l, f x < g x :=
  let ‚ü®_a, ha, _b, hb, h‚ü© := hyz.exists_disjoint_Iio_Ioi
  (hg.eventually (Ioi_mem_nhds hb)).mp <| (hf.eventually (Iio_mem_nhds ha)).mono fun _ h‚ÇÅ h‚ÇÇ =>
    h _ h‚ÇÅ _ h‚ÇÇ
#align tendsto.eventually_lt Filter.Tendsto.eventually_lt

nonrec theorem ContinuousAt.eventually_lt {x‚ÇÄ : Œ≤} (hf : ContinuousAt f x‚ÇÄ) (hg : ContinuousAt g x‚ÇÄ)
    (hfg : f x‚ÇÄ < g x‚ÇÄ) : ‚àÄ·∂† x in ùìù x‚ÇÄ, f x < g x :=
  hf.eventually_lt hg hfg
#align continuous_at.eventually_lt ContinuousAt.eventually_lt

@[continuity, fun_prop]
protected theorem Continuous.min (hf : Continuous f) (hg : Continuous g) :
    Continuous fun b => min (f b) (g b) := by
  simp only [min_def]
  exact hf.if_le hg hf hg fun x => id
#align continuous.min Continuous.min

@[continuity, fun_prop]
protected theorem Continuous.max (hf : Continuous f) (hg : Continuous g) :
    Continuous fun b => max (f b) (g b) :=
  Continuous.min (Œ± := Œ±·µí·µà) hf hg
#align continuous.max Continuous.max

end

theorem continuous_min : Continuous fun p : Œ± √ó Œ± => min p.1 p.2 :=
  continuous_fst.min continuous_snd
#align continuous_min continuous_min

theorem continuous_max : Continuous fun p : Œ± √ó Œ± => max p.1 p.2 :=
  continuous_fst.max continuous_snd
#align continuous_max continuous_max

protected theorem Filter.Tendsto.max {b : Filter Œ≤} {a‚ÇÅ a‚ÇÇ : Œ±} (hf : Tendsto f b (ùìù a‚ÇÅ))
    (hg : Tendsto g b (ùìù a‚ÇÇ)) : Tendsto (fun b => max (f b) (g b)) b (ùìù (max a‚ÇÅ a‚ÇÇ)) :=
  (continuous_max.tendsto (a‚ÇÅ, a‚ÇÇ)).comp (hf.prod_mk_nhds hg)
#align filter.tendsto.max Filter.Tendsto.max

protected theorem Filter.Tendsto.min {b : Filter Œ≤} {a‚ÇÅ a‚ÇÇ : Œ±} (hf : Tendsto f b (ùìù a‚ÇÅ))
    (hg : Tendsto g b (ùìù a‚ÇÇ)) : Tendsto (fun b => min (f b) (g b)) b (ùìù (min a‚ÇÅ a‚ÇÇ)) :=
  (continuous_min.tendsto (a‚ÇÅ, a‚ÇÇ)).comp (hf.prod_mk_nhds hg)
#align filter.tendsto.min Filter.Tendsto.min

protected theorem Filter.Tendsto.max_right {l : Filter Œ≤} {a : Œ±} (h : Tendsto f l (ùìù a)) :
    Tendsto (fun i => max a (f i)) l (ùìù a) := by
  convert ((continuous_max.comp (@Continuous.Prod.mk Œ± Œ± _ _ a)).tendsto a).comp h
  simp
#align filter.tendsto.max_right Filter.Tendsto.max_right

protected theorem Filter.Tendsto.max_left {l : Filter Œ≤} {a : Œ±} (h : Tendsto f l (ùìù a)) :
    Tendsto (fun i => max (f i) a) l (ùìù a) := by
  simp_rw [max_comm _ a]
  exact h.max_right
#align filter.tendsto.max_left Filter.Tendsto.max_left

theorem Filter.tendsto_nhds_max_right {l : Filter Œ≤} {a : Œ±} (h : Tendsto f l (ùìù[>] a)) :
    Tendsto (fun i => max a (f i)) l (ùìù[>] a) := by
  obtain ‚ü®h‚ÇÅ : Tendsto f l (ùìù a), h‚ÇÇ : ‚àÄ·∂† i in l, f i ‚àà Ioi a‚ü© := tendsto_nhdsWithin_iff.mp h
  exact tendsto_nhdsWithin_iff.mpr ‚ü®h‚ÇÅ.max_right, h‚ÇÇ.mono fun i hi => lt_max_of_lt_right hi‚ü©
#align filter.tendsto_nhds_max_right Filter.tendsto_nhds_max_right

theorem Filter.tendsto_nhds_max_left {l : Filter Œ≤} {a : Œ±} (h : Tendsto f l (ùìù[>] a)) :
    Tendsto (fun i => max (f i) a) l (ùìù[>] a) := by
  simp_rw [max_comm _ a]
  exact Filter.tendsto_nhds_max_right h
#align filter.tendsto_nhds_max_left Filter.tendsto_nhds_max_left

theorem Filter.Tendsto.min_right {l : Filter Œ≤} {a : Œ±} (h : Tendsto f l (ùìù a)) :
    Tendsto (fun i => min a (f i)) l (ùìù a) :=
  Filter.Tendsto.max_right (Œ± := Œ±·µí·µà) h
#align filter.tendsto.min_right Filter.Tendsto.min_right

theorem Filter.Tendsto.min_left {l : Filter Œ≤} {a : Œ±} (h : Tendsto f l (ùìù a)) :
    Tendsto (fun i => min (f i) a) l (ùìù a) :=
  Filter.Tendsto.max_left (Œ± := Œ±·µí·µà) h
#align filter.tendsto.min_left Filter.Tendsto.min_left

theorem Filter.tendsto_nhds_min_right {l : Filter Œ≤} {a : Œ±} (h : Tendsto f l (ùìù[<] a)) :
    Tendsto (fun i => min a (f i)) l (ùìù[<] a) :=
  Filter.tendsto_nhds_max_right (Œ± := Œ±·µí·µà) h
#align filter.tendsto_nhds_min_right Filter.tendsto_nhds_min_right

theorem Filter.tendsto_nhds_min_left {l : Filter Œ≤} {a : Œ±} (h : Tendsto f l (ùìù[<] a)) :
    Tendsto (fun i => min (f i) a) l (ùìù[<] a) :=
  Filter.tendsto_nhds_max_left (Œ± := Œ±·µí·µà) h
#align filter.tendsto_nhds_min_left Filter.tendsto_nhds_min_left

theorem Dense.exists_between [DenselyOrdered Œ±] {s : Set Œ±} (hs : Dense s) {x y : Œ±} (h : x < y) :
    ‚àÉ z ‚àà s, z ‚àà Ioo x y :=
  hs.exists_mem_open isOpen_Ioo (nonempty_Ioo.2 h)
#align dense.exists_between Dense.exists_between

theorem Dense.Ioi_eq_biUnion [DenselyOrdered Œ±] {s : Set Œ±} (hs : Dense s) (x : Œ±) :
    Ioi x = ‚ãÉ y ‚àà s ‚à© Ioi x, Ioi y := by
  refine Subset.antisymm (fun z hz ‚Ü¶ ?_) (iUnion‚ÇÇ_subset fun y hy ‚Ü¶ Ioi_subset_Ioi (le_of_lt hy.2))
  rcases hs.exists_between hz with ‚ü®y, hys, hxy, hyz‚ü©
  exact mem_iUnion‚ÇÇ.2 ‚ü®y, ‚ü®hys, hxy‚ü©, hyz‚ü©

theorem Dense.Iio_eq_biUnion [DenselyOrdered Œ±] {s : Set Œ±} (hs : Dense s) (x : Œ±) :
    Iio x = ‚ãÉ y ‚àà s ‚à© Iio x, Iio y :=
  Dense.Ioi_eq_biUnion (Œ± := Œ±·µí·µà) hs x

end LinearOrder

end OrderClosedTopology

instance [Preorder Œ±] [TopologicalSpace Œ±] [OrderClosedTopology Œ±] [Preorder Œ≤] [TopologicalSpace Œ≤]
    [OrderClosedTopology Œ≤] : OrderClosedTopology (Œ± √ó Œ≤) :=
  ‚ü®(isClosed_le continuous_fst.fst continuous_snd.fst).inter
    (isClosed_le continuous_fst.snd continuous_snd.snd)‚ü©

instance {Œπ : Type*} {Œ± : Œπ ‚Üí Type*} [‚àÄ i, Preorder (Œ± i)] [‚àÄ i, TopologicalSpace (Œ± i)]
    [‚àÄ i, OrderClosedTopology (Œ± i)] : OrderClosedTopology (‚àÄ i, Œ± i) := by
  constructor
  simp only [Pi.le_def, setOf_forall]
  exact isClosed_iInter fun i => isClosed_le (continuous_apply i).fst' (continuous_apply i).snd'

instance Pi.orderClosedTopology' [Preorder Œ≤] [TopologicalSpace Œ≤] [OrderClosedTopology Œ≤] :
    OrderClosedTopology (Œ± ‚Üí Œ≤) :=
  inferInstance
#align pi.order_closed_topology' Pi.orderClosedTopology'
