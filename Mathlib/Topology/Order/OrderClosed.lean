/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro, Yury Kudryashov
-/
import Mathlib.Topology.Order.LeftRight
import Mathlib.Topology.Separation.Hausdorff

/-!
# Order-closed topologies

In this file we introduce 3 typeclass mixins that relate topology and order structures:

- `ClosedIicTopology` says that all the intervals $(-‚àû, a]$ (formally, `Set.Iic a`)
  are closed sets;
- `ClosedIciTopology` says that all the intervals $[a, +‚àû)$ (formally, `Set.Ici a`)
  are closed sets;
- `OrderClosedTopology` says that the set of points `(x, y)` such that `x ‚â§ y`
  is closed in the product topology.

The last predicate implies the first two.

We prove many basic properties of such topologies.

## Main statements

This file contains the proofs of the following facts.
For exact requirements
(`OrderClosedTopology` vs `ClosedIciTopology` vs `ClosedIicTopology,
`Preorder` vs `PartialOrder` vs `LinearOrder` etc)
see their statements.

### Open / closed sets

* `isOpen_lt` : if `f` and `g` are continuous functions, then `{x | f x < g x}` is open;
* `isOpen_Iio`, `isOpen_Ioi`, `isOpen_Ioo` : open intervals are open;
* `isClosed_le` : if `f` and `g` are continuous functions, then `{x | f x ‚â§ g x}` is closed;
* `isClosed_Iic`, `isClosed_Ici`, `isClosed_Icc` : closed intervals are closed;
* `frontier_le_subset_eq`, `frontier_lt_subset_eq` : frontiers of both `{x | f x ‚â§ g x}`
  and `{x | f x < g x}` are included by `{x | f x = g x}`;

### Convergence and inequalities

* `le_of_tendsto_of_tendsto` : if `f` converges to `a`, `g` converges to `b`, and eventually
  `f x ‚â§ g x`, then `a ‚â§ b`
* `le_of_tendsto`, `ge_of_tendsto` : if `f` converges to `a` and eventually `f x ‚â§ b`
  (resp., `b ‚â§ f x`), then `a ‚â§ b` (resp., `b ‚â§ a`); we also provide primed versions
  that assume the inequalities to hold for all `x`.

### Min, max, `sSup` and `sInf`

* `Continuous.min`, `Continuous.max`: pointwise `min`/`max` of two continuous functions is
  continuous.
* `Tendsto.min`, `Tendsto.max` : if `f` tends to `a` and `g` tends to `b`, then their pointwise
  `min`/`max` tend to `min a b` and `max a b`, respectively.
-/

open Set Filter
open OrderDual (toDual)
open scoped Topology

universe u v w
variable {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w}

/-- If `Œ±` is a topological space and a preorder, `ClosedIicTopology Œ±` means that `Iic a` is
closed for all `a : Œ±`. -/
class ClosedIicTopology (Œ± : Type*) [TopologicalSpace Œ±] [Preorder Œ±] : Prop where
  /-- For any `a`, the set `(-‚àû, a]` is closed. -/
  isClosed_Iic (a : Œ±) : IsClosed (Iic a)

/-- If `Œ±` is a topological space and a preorder, `ClosedIciTopology Œ±` means that `Ici a` is
closed for all `a : Œ±`. -/
class ClosedIciTopology (Œ± : Type*) [TopologicalSpace Œ±] [Preorder Œ±] : Prop where
  /-- For any `a`, the set `[a, +‚àû)` is closed. -/
  isClosed_Ici (a : Œ±) : IsClosed (Ici a)

/-- A topology on a set which is both a topological space and a preorder is _order-closed_ if the
set of points `(x, y)` with `x ‚â§ y` is closed in the product space. We introduce this as a mixin.
This property is satisfied for the order topology on a linear order, but it can be satisfied more
generally, and suffices to derive many interesting properties relating order and topology. -/
class OrderClosedTopology (Œ± : Type*) [TopologicalSpace Œ±] [Preorder Œ±] : Prop where
  /-- The set `{ (x, y) | x ‚â§ y }` is a closed set. -/
  isClosed_le' : IsClosed { p : Œ± √ó Œ± | p.1 ‚â§ p.2 }

instance [TopologicalSpace Œ±] [h : FirstCountableTopology Œ±] : FirstCountableTopology Œ±·µí·µà := h
instance [TopologicalSpace Œ±] [h : SecondCountableTopology Œ±] : SecondCountableTopology Œ±·µí·µà := h

theorem Dense.orderDual [TopologicalSpace Œ±] {s : Set Œ±} (hs : Dense s) :
    Dense (OrderDual.ofDual ‚Åª¬π' s) :=
  hs

section General
variable [TopologicalSpace Œ±] [Preorder Œ±] {s : Set Œ±}

protected lemma BddAbove.of_closure : BddAbove (closure s) ‚Üí BddAbove s :=
  BddAbove.mono subset_closure

protected lemma BddBelow.of_closure : BddBelow (closure s) ‚Üí BddBelow s :=
  BddBelow.mono subset_closure

end General

section ClosedIicTopology

section Preorder

variable [TopologicalSpace Œ±] [Preorder Œ±] [ClosedIicTopology Œ±] {f : Œ≤ ‚Üí Œ±} {a b : Œ±} {s : Set Œ±}

theorem isClosed_Iic : IsClosed (Iic a) :=
  ClosedIicTopology.isClosed_Iic a

instance : ClosedIciTopology Œ±·µí·µà where
  isClosed_Ici _ := isClosed_Iic (Œ± := Œ±)

@[simp]
theorem closure_Iic (a : Œ±) : closure (Iic a) = Iic a :=
  isClosed_Iic.closure_eq

theorem le_of_tendsto_of_frequently {x : Filter Œ≤} (lim : Tendsto f x (ùìù a))
    (h : ‚àÉ·∂† c in x, f c ‚â§ b) : a ‚â§ b :=
  isClosed_Iic.mem_of_frequently_of_tendsto h lim

theorem le_of_tendsto {x : Filter Œ≤} [NeBot x] (lim : Tendsto f x (ùìù a))
    (h : ‚àÄ·∂† c in x, f c ‚â§ b) : a ‚â§ b :=
  isClosed_Iic.mem_of_tendsto lim h

theorem le_of_tendsto' {x : Filter Œ≤} [NeBot x] (lim : Tendsto f x (ùìù a))
    (h : ‚àÄ c, f c ‚â§ b) : a ‚â§ b :=
  le_of_tendsto lim (Eventually.of_forall h)

@[simp] lemma upperBounds_closure (s : Set Œ±) : upperBounds (closure s : Set Œ±) = upperBounds s :=
  ext fun a ‚Ü¶ by simp_rw [mem_upperBounds_iff_subset_Iic, isClosed_Iic.closure_subset_iff]

@[simp] lemma bddAbove_closure : BddAbove (closure s) ‚Üî BddAbove s := by
  simp_rw [BddAbove, upperBounds_closure]

protected alias ‚ü®_, BddAbove.closure‚ü© := bddAbove_closure

@[simp]
theorem disjoint_nhds_atBot_iff : Disjoint (ùìù a) atBot ‚Üî ¬¨IsBot a := by
  constructor
  ¬∑ intro hd hbot
    rw [hbot.atBot_eq, disjoint_principal_right] at hd
    exact mem_of_mem_nhds hd le_rfl
  ¬∑ simp only [IsBot, not_forall]
    rintro ‚ü®b, hb‚ü©
    refine disjoint_of_disjoint_of_mem disjoint_compl_left ?_ (Iic_mem_atBot b)
    exact isClosed_Iic.isOpen_compl.mem_nhds hb

theorem IsLUB.range_of_tendsto {F : Filter Œ≤} [F.NeBot] (hle : ‚àÄ i, f i ‚â§ a)
    (hlim : Tendsto f F (ùìù a)) : IsLUB (range f) a :=
  ‚ü®forall_mem_range.mpr hle, fun _c hc ‚Ü¶ le_of_tendsto' hlim fun i ‚Ü¶ hc <| mem_range_self i‚ü©

end Preorder

section NoBotOrder

variable [Preorder Œ±] [NoBotOrder Œ±] [TopologicalSpace Œ±] [ClosedIicTopology Œ±] {a : Œ±}
  {l : Filter Œ≤} [NeBot l] {f : Œ≤ ‚Üí Œ±}

theorem disjoint_nhds_atBot (a : Œ±) : Disjoint (ùìù a) atBot := by simp

@[simp]
theorem inf_nhds_atBot (a : Œ±) : ùìù a ‚äì atBot = ‚ä• := (disjoint_nhds_atBot a).eq_bot

theorem not_tendsto_nhds_of_tendsto_atBot (hf : Tendsto f l atBot) (a : Œ±) : ¬¨Tendsto f l (ùìù a) :=
  hf.not_tendsto (disjoint_nhds_atBot a).symm

theorem not_tendsto_atBot_of_tendsto_nhds (hf : Tendsto f l (ùìù a)) : ¬¨Tendsto f l atBot :=
  hf.not_tendsto (disjoint_nhds_atBot a)

end NoBotOrder

theorem iSup_eq_of_forall_le_of_tendsto {Œπ : Type*} {F : Filter Œπ} [Filter.NeBot F]
    [ConditionallyCompleteLattice Œ±] [TopologicalSpace Œ±] [ClosedIicTopology Œ±]
    {a : Œ±} {f : Œπ ‚Üí Œ±} (hle : ‚àÄ i, f i ‚â§ a) (hlim : Filter.Tendsto f F (ùìù a)) :
    ‚®Ü i, f i = a :=
  have := F.nonempty_of_neBot
  (IsLUB.range_of_tendsto hle hlim).ciSup_eq

theorem iUnion_Iic_eq_Iio_of_lt_of_tendsto {Œπ : Type*} {F : Filter Œπ} [F.NeBot]
    [ConditionallyCompleteLinearOrder Œ±] [TopologicalSpace Œ±] [ClosedIicTopology Œ±]
    {a : Œ±} {f : Œπ ‚Üí Œ±} (hlt : ‚àÄ i, f i < a) (hlim : Tendsto f F (ùìù a)) :
    ‚ãÉ i : Œπ, Iic (f i) = Iio a := by
  have obs : a ‚àâ range f := by
    rw [mem_range]
    rintro ‚ü®i, rfl‚ü©
    exact (hlt i).false
  rw [‚Üê biUnion_range, (IsLUB.range_of_tendsto (le_of_lt <| hlt ¬∑) hlim).biUnion_Iic_eq_Iio obs]

section LinearOrder

variable [TopologicalSpace Œ±] [LinearOrder Œ±] [ClosedIicTopology Œ±] [TopologicalSpace Œ≤]
  {a b c : Œ±} {f : Œ± ‚Üí Œ≤}

theorem isOpen_Ioi : IsOpen (Ioi a) := by
  rw [‚Üê compl_Iic]
  exact isClosed_Iic.isOpen_compl

@[simp]
theorem interior_Ioi : interior (Ioi a) = Ioi a :=
  isOpen_Ioi.interior_eq

theorem Ioi_mem_nhds (h : a < b) : Ioi a ‚àà ùìù b := IsOpen.mem_nhds isOpen_Ioi h

theorem eventually_gt_nhds (hab : b < a) : ‚àÄ·∂† x in ùìù a, b < x := Ioi_mem_nhds hab

theorem Ici_mem_nhds (h : a < b) : Ici a ‚àà ùìù b :=
  mem_of_superset (Ioi_mem_nhds h) Ioi_subset_Ici_self

theorem eventually_ge_nhds (hab : b < a) : ‚àÄ·∂† x in ùìù a, b ‚â§ x := Ici_mem_nhds hab

theorem Filter.Tendsto.eventually_const_lt {l : Filter Œ≥} {f : Œ≥ ‚Üí Œ±} {u v : Œ±} (hv : u < v)
    (h : Filter.Tendsto f l (ùìù v)) : ‚àÄ·∂† a in l, u < f a :=
  h.eventually <| eventually_gt_nhds hv

@[deprecated (since := "2024-11-17")]
alias eventually_gt_of_tendsto_gt := Filter.Tendsto.eventually_const_lt

theorem Filter.Tendsto.eventually_const_le {l : Filter Œ≥} {f : Œ≥ ‚Üí Œ±} {u v : Œ±} (hv : u < v)
    (h : Tendsto f l (ùìù v)) : ‚àÄ·∂† a in l, u ‚â§ f a :=
  h.eventually <| eventually_ge_nhds hv

@[deprecated (since := "2024-11-17")]
alias eventually_ge_of_tendsto_gt := Filter.Tendsto.eventually_const_le

protected theorem Dense.exists_gt [NoMaxOrder Œ±] {s : Set Œ±} (hs : Dense s) (x : Œ±) :
    ‚àÉ y ‚àà s, x < y :=
  hs.exists_mem_open isOpen_Ioi (exists_gt x)

protected theorem Dense.exists_ge [NoMaxOrder Œ±] {s : Set Œ±} (hs : Dense s) (x : Œ±) :
    ‚àÉ y ‚àà s, x ‚â§ y :=
  (hs.exists_gt x).imp fun _ h ‚Ü¶ ‚ü®h.1, h.2.le‚ü©

theorem Dense.exists_ge' {s : Set Œ±} (hs : Dense s) (htop : ‚àÄ x, IsTop x ‚Üí x ‚àà s) (x : Œ±) :
    ‚àÉ y ‚àà s, x ‚â§ y := by
  by_cases hx : IsTop x
  ¬∑ exact ‚ü®x, htop x hx, le_rfl‚ü©
  ¬∑ simp only [IsTop, not_forall, not_le] at hx
    rcases hs.exists_mem_open isOpen_Ioi hx with ‚ü®y, hys, hy : x < y‚ü©
    exact ‚ü®y, hys, hy.le‚ü©

/-!
### Left neighborhoods on a `ClosedIicTopology`

Limits to the left of real functions are defined in terms of neighborhoods to the left,
either open or closed, i.e., members of `ùìù[<] a` and `ùìù[‚â§] a`.
Here we prove that all left-neighborhoods of a point are equal,
and we prove other useful characterizations which require the stronger hypothesis `OrderTopology Œ±`
in another file.
-/

/-!
#### Point excluded
-/

theorem Ioo_mem_nhdsLT (H : a < b) : Ioo a b ‚àà ùìù[<] b := by
  simpa only [‚Üê Iio_inter_Ioi] using inter_mem_nhdsWithin _ (Ioi_mem_nhds H)

@[deprecated (since := "2024-12-21")] alias Ioo_mem_nhdsWithin_Iio' := Ioo_mem_nhdsLT

theorem Ioo_mem_nhdsLT_of_mem (H : b ‚àà Ioc a c) : Ioo a c ‚àà ùìù[<] b :=
  mem_of_superset (Ioo_mem_nhdsLT H.1) <| Ioo_subset_Ioo_right H.2

@[deprecated (since := "2024-12-21")] alias Ioo_mem_nhdsWithin_Iio := Ioo_mem_nhdsLT_of_mem

protected theorem CovBy.nhdsLT (h : a ‚ãñ b) : ùìù[<] b = ‚ä• :=
  empty_mem_iff_bot.mp <| h.Ioo_eq ‚ñ∏ Ioo_mem_nhdsLT h.1

@[deprecated (since := "2024-12-21")] protected alias CovBy.nhdsWithin_Iio := CovBy.nhdsLT

protected theorem PredOrder.nhdsLT [PredOrder Œ±] : ùìù[<] a = ‚ä• := by
  if h : IsMin a then simp [h.Iio_eq]
  else exact (Order.pred_covBy_of_not_isMin h).nhdsLT

@[deprecated (since := "2024-12-21")] protected alias PredOrder.nhdsWithin_Iio := PredOrder.nhdsLT

theorem PredOrder.nhdsGT_eq_nhdsNE [PredOrder Œ±] (a : Œ±) : ùìù[>] a = ùìù[‚â†] a := by
  rw [‚Üê nhdsLT_sup_nhdsGT, PredOrder.nhdsLT, bot_sup_eq]

theorem PredOrder.nhdsGE_eq_nhds [PredOrder Œ±] (a : Œ±) : ùìù[‚â•] a = ùìù a := by
  rw [‚Üê nhdsLT_sup_nhdsGE, PredOrder.nhdsLT, bot_sup_eq]

theorem Ico_mem_nhdsLT_of_mem (H : b ‚àà Ioc a c) : Ico a c ‚àà ùìù[<] b :=
  mem_of_superset (Ioo_mem_nhdsLT_of_mem H) Ioo_subset_Ico_self

@[deprecated (since := "2024-12-21")] alias Ico_mem_nhdsWithin_Iio := Ico_mem_nhdsLT_of_mem

theorem Ico_mem_nhdsLT (H : a < b) : Ico a b ‚àà ùìù[<] b := Ico_mem_nhdsLT_of_mem ‚ü®H, le_rfl‚ü©

@[deprecated (since := "2024-12-21")] alias Ico_mem_nhdsWithin_Iio' := Ico_mem_nhdsLT

theorem Ioc_mem_nhdsLT_of_mem (H : b ‚àà Ioc a c) : Ioc a c ‚àà ùìù[<] b :=
  mem_of_superset (Ioo_mem_nhdsLT_of_mem H) Ioo_subset_Ioc_self

@[deprecated (since := "2024-12-21")] alias Ioc_mem_nhdsWithin_Iio := Ioc_mem_nhdsLT_of_mem

theorem Ioc_mem_nhdsLT (H : a < b) : Ioc a b ‚àà ùìù[<] b := Ioc_mem_nhdsLT_of_mem ‚ü®H, le_rfl‚ü©

@[deprecated (since := "2024-12-21")] alias Ioc_mem_nhdsWithin_Iio' := Ioc_mem_nhdsLT

theorem Icc_mem_nhdsLT_of_mem (H : b ‚àà Ioc a c) : Icc a c ‚àà ùìù[<] b :=
  mem_of_superset (Ioo_mem_nhdsLT_of_mem H) Ioo_subset_Icc_self

@[deprecated (since := "2024-12-21")] alias Icc_mem_nhdsWithin_Iio := Icc_mem_nhdsLT_of_mem

theorem Icc_mem_nhdsLT (H : a < b) : Icc a b ‚àà ùìù[<] b := Icc_mem_nhdsLT_of_mem ‚ü®H, le_rfl‚ü©

@[deprecated (since := "2024-12-21")] alias Icc_mem_nhdsWithin_Iio' := Icc_mem_nhdsLT

@[simp]
theorem nhdsWithin_Ico_eq_nhdsLT (h : a < b) : ùìù[Ico a b] b = ùìù[<] b :=
  nhdsWithin_inter_of_mem <| nhdsWithin_le_nhds <| Ici_mem_nhds h

@[deprecated (since := "2024-12-21")]
alias nhdsWithin_Ico_eq_nhdsWithin_Iio := nhdsWithin_Ico_eq_nhdsLT

@[simp]
theorem nhdsWithin_Ioo_eq_nhdsLT (h : a < b) : ùìù[Ioo a b] b = ùìù[<] b :=
  nhdsWithin_inter_of_mem <| nhdsWithin_le_nhds <| Ioi_mem_nhds h

@[deprecated (since := "2024-12-21")]
alias nhdsWithin_Ioo_eq_nhdsWithin_Iio := nhdsWithin_Ioo_eq_nhdsLT

@[simp]
theorem continuousWithinAt_Ico_iff_Iio (h : a < b) :
    ContinuousWithinAt f (Ico a b) b ‚Üî ContinuousWithinAt f (Iio b) b := by
  simp only [ContinuousWithinAt, nhdsWithin_Ico_eq_nhdsLT h]

@[simp]
theorem continuousWithinAt_Ioo_iff_Iio (h : a < b) :
    ContinuousWithinAt f (Ioo a b) b ‚Üî ContinuousWithinAt f (Iio b) b := by
  simp only [ContinuousWithinAt, nhdsWithin_Ioo_eq_nhdsLT h]

/-!
#### Point included
-/

protected theorem CovBy.nhdsLE (H : a ‚ãñ b) : ùìù[‚â§] b = pure b := by
  rw [‚Üê Iio_insert, nhdsWithin_insert, H.nhdsLT, sup_bot_eq]

@[deprecated (since := "2024-12-21")]
protected alias CovBy.nhdsWithin_Iic := CovBy.nhdsLE

protected theorem PredOrder.nhdsLE [PredOrder Œ±] : ùìù[‚â§] b = pure b := by
  rw [‚Üê Iio_insert, nhdsWithin_insert, PredOrder.nhdsLT, sup_bot_eq]

@[deprecated (since := "2024-12-21")]
protected alias PredOrder.nhdsWithin_Iic := PredOrder.nhdsLE

theorem Ioc_mem_nhdsLE (H : a < b) : Ioc a b ‚àà ùìù[‚â§] b :=
  inter_mem (nhdsWithin_le_nhds <| Ioi_mem_nhds H) self_mem_nhdsWithin

@[deprecated (since := "2024-12-21")] alias Ioc_mem_nhdsWithin_Iic' := Ioc_mem_nhdsLE

theorem Ioo_mem_nhdsLE_of_mem (H : b ‚àà Ioo a c) : Ioo a c ‚àà ùìù[‚â§] b :=
  mem_of_superset (Ioc_mem_nhdsLE H.1) <| Ioc_subset_Ioo_right H.2

@[deprecated (since := "2024-12-21")] alias Ioo_mem_nhdsWithin_Iic := Ioo_mem_nhdsLE_of_mem

theorem Ico_mem_nhdsLE_of_mem (H : b ‚àà Ioo a c) : Ico a c ‚àà ùìù[‚â§] b :=
  mem_of_superset (Ioo_mem_nhdsLE_of_mem H) Ioo_subset_Ico_self

@[deprecated (since := "2024-12-22")]
alias Ico_mem_nhdsWithin_Iic := Ico_mem_nhdsLE_of_mem

theorem Ioc_mem_nhdsLE_of_mem (H : b ‚àà Ioc a c) : Ioc a c ‚àà ùìù[‚â§] b :=
  mem_of_superset (Ioc_mem_nhdsLE H.1) <| Ioc_subset_Ioc_right H.2

@[deprecated (since := "2024-12-22")]
alias Ioc_mem_nhdsWithin_Iic := Ioc_mem_nhdsLE_of_mem

theorem Icc_mem_nhdsLE_of_mem (H : b ‚àà Ioc a c) : Icc a c ‚àà ùìù[‚â§] b :=
  mem_of_superset (Ioc_mem_nhdsLE_of_mem H) Ioc_subset_Icc_self

@[deprecated (since := "2024-12-22")]
alias Icc_mem_nhdsWithin_Iic := Icc_mem_nhdsLE_of_mem

theorem Icc_mem_nhdsLE (H : a < b) : Icc a b ‚àà ùìù[‚â§] b := Icc_mem_nhdsLE_of_mem ‚ü®H, le_rfl‚ü©

@[deprecated (since := "2024-12-22")]
alias Icc_mem_nhdsWithin_Iic' := Icc_mem_nhdsLE

@[simp]
theorem nhdsWithin_Icc_eq_nhdsLE (h : a < b) : ùìù[Icc a b] b = ùìù[‚â§] b :=
  nhdsWithin_inter_of_mem <| nhdsWithin_le_nhds <| Ici_mem_nhds h

@[deprecated (since := "2024-12-22")]
alias nhdsWithin_Icc_eq_nhdsWithin_Iic := nhdsWithin_Icc_eq_nhdsLE

@[simp]
theorem nhdsWithin_Ioc_eq_nhdsLE (h : a < b) : ùìù[Ioc a b] b = ùìù[‚â§] b :=
  nhdsWithin_inter_of_mem <| nhdsWithin_le_nhds <| Ioi_mem_nhds h

@[deprecated (since := "2024-12-22")]
alias nhdsWithin_Ioc_eq_nhdsWithin_Iic := nhdsWithin_Ioc_eq_nhdsLE

@[simp]
theorem continuousWithinAt_Icc_iff_Iic (h : a < b) :
    ContinuousWithinAt f (Icc a b) b ‚Üî ContinuousWithinAt f (Iic b) b := by
  simp only [ContinuousWithinAt, nhdsWithin_Icc_eq_nhdsLE h]

@[simp]
theorem continuousWithinAt_Ioc_iff_Iic (h : a < b) :
    ContinuousWithinAt f (Ioc a b) b ‚Üî ContinuousWithinAt f (Iic b) b := by
  simp only [ContinuousWithinAt, nhdsWithin_Ioc_eq_nhdsLE h]

end LinearOrder

end ClosedIicTopology

section ClosedIciTopology

section Preorder

variable [TopologicalSpace Œ±] [Preorder Œ±] [ClosedIciTopology Œ±] {f : Œ≤ ‚Üí Œ±} {a b : Œ±} {s : Set Œ±}

theorem isClosed_Ici {a : Œ±} : IsClosed (Ici a) :=
  ClosedIciTopology.isClosed_Ici a

instance : ClosedIicTopology Œ±·µí·µà where
  isClosed_Iic _ := isClosed_Ici (Œ± := Œ±)

@[simp]
theorem closure_Ici (a : Œ±) : closure (Ici a) = Ici a :=
  isClosed_Ici.closure_eq

lemma ge_of_tendsto_of_frequently {x : Filter Œ≤} (lim : Tendsto f x (ùìù a))
    (h : ‚àÉ·∂† c in x, b ‚â§ f c) : b ‚â§ a :=
  isClosed_Ici.mem_of_frequently_of_tendsto h lim

theorem ge_of_tendsto {x : Filter Œ≤} [NeBot x] (lim : Tendsto f x (ùìù a))
    (h : ‚àÄ·∂† c in x, b ‚â§ f c) : b ‚â§ a :=
  isClosed_Ici.mem_of_tendsto lim h

theorem ge_of_tendsto' {x : Filter Œ≤} [NeBot x] (lim : Tendsto f x (ùìù a))
    (h : ‚àÄ c, b ‚â§ f c) : b ‚â§ a :=
  ge_of_tendsto lim (Eventually.of_forall h)

@[simp] lemma lowerBounds_closure (s : Set Œ±) : lowerBounds (closure s : Set Œ±) = lowerBounds s :=
  ext fun a ‚Ü¶ by simp_rw [mem_lowerBounds_iff_subset_Ici, isClosed_Ici.closure_subset_iff]

@[simp] lemma bddBelow_closure : BddBelow (closure s) ‚Üî BddBelow s := by
  simp_rw [BddBelow, lowerBounds_closure]

protected alias ‚ü®_, BddBelow.closure‚ü© := bddBelow_closure

@[simp]
theorem disjoint_nhds_atTop_iff : Disjoint (ùìù a) atTop ‚Üî ¬¨IsTop a :=
  disjoint_nhds_atBot_iff (Œ± := Œ±·µí·µà)

theorem IsGLB.range_of_tendsto {F : Filter Œ≤} [F.NeBot] (hle : ‚àÄ i, a ‚â§ f i)
    (hlim : Tendsto f F (ùìù a)) : IsGLB (range f) a :=
  IsLUB.range_of_tendsto (Œ± := Œ±·µí·µà) hle hlim

end Preorder

section NoTopOrder

variable [Preorder Œ±] [NoTopOrder Œ±] [TopologicalSpace Œ±] [ClosedIciTopology Œ±] {a : Œ±}
  {l : Filter Œ≤} [NeBot l] {f : Œ≤ ‚Üí Œ±}

theorem disjoint_nhds_atTop (a : Œ±) : Disjoint (ùìù a) atTop := disjoint_nhds_atBot (toDual a)

@[simp]
theorem inf_nhds_atTop (a : Œ±) : ùìù a ‚äì atTop = ‚ä• := (disjoint_nhds_atTop a).eq_bot

theorem not_tendsto_nhds_of_tendsto_atTop (hf : Tendsto f l atTop) (a : Œ±) : ¬¨Tendsto f l (ùìù a) :=
  hf.not_tendsto (disjoint_nhds_atTop a).symm

theorem not_tendsto_atTop_of_tendsto_nhds (hf : Tendsto f l (ùìù a)) : ¬¨Tendsto f l atTop :=
  hf.not_tendsto (disjoint_nhds_atTop a)

end NoTopOrder

theorem iInf_eq_of_forall_le_of_tendsto {Œπ : Type*} {F : Filter Œπ} [F.NeBot]
    [ConditionallyCompleteLattice Œ±] [TopologicalSpace Œ±] [ClosedIciTopology Œ±]
    {a : Œ±} {f : Œπ ‚Üí Œ±} (hle : ‚àÄ i, a ‚â§ f i) (hlim : Tendsto f F (ùìù a)) :
    ‚®Ö i, f i = a :=
  iSup_eq_of_forall_le_of_tendsto (Œ± := Œ±·µí·µà) hle hlim

theorem iUnion_Ici_eq_Ioi_of_lt_of_tendsto {Œπ : Type*} {F : Filter Œπ} [F.NeBot]
    [ConditionallyCompleteLinearOrder Œ±] [TopologicalSpace Œ±] [ClosedIciTopology Œ±]
    {a : Œ±} {f : Œπ ‚Üí Œ±} (hlt : ‚àÄ i, a < f i) (hlim : Tendsto f F (ùìù a)) :
    ‚ãÉ i : Œπ, Ici (f i) = Ioi a :=
  iUnion_Iic_eq_Iio_of_lt_of_tendsto (Œ± := Œ±·µí·µà) hlt hlim

section LinearOrder

variable [TopologicalSpace Œ±] [LinearOrder Œ±] [ClosedIciTopology Œ±] [TopologicalSpace Œ≤]
  {a b c : Œ±} {f : Œ± ‚Üí Œ≤}

theorem isOpen_Iio : IsOpen (Iio a) := isOpen_Ioi (Œ± := Œ±·µí·µà)

@[simp] theorem interior_Iio : interior (Iio a) = Iio a := isOpen_Iio.interior_eq

theorem Iio_mem_nhds (h : a < b) : Iio b ‚àà ùìù a := isOpen_Iio.mem_nhds h

theorem eventually_lt_nhds (hab : a < b) : ‚àÄ·∂† x in ùìù a, x < b := Iio_mem_nhds hab

theorem Iic_mem_nhds (h : a < b) : Iic b ‚àà ùìù a :=
  mem_of_superset (Iio_mem_nhds h) Iio_subset_Iic_self

theorem eventually_le_nhds (hab : a < b) : ‚àÄ·∂† x in ùìù a, x ‚â§ b := Iic_mem_nhds hab

theorem Filter.Tendsto.eventually_lt_const {l : Filter Œ≥} {f : Œ≥ ‚Üí Œ±} {u v : Œ±} (hv : v < u)
    (h : Filter.Tendsto f l (ùìù v)) : ‚àÄ·∂† a in l, f a < u :=
  h.eventually <| eventually_lt_nhds hv

@[deprecated (since := "2024-11-17")]
alias eventually_lt_of_tendsto_lt := Filter.Tendsto.eventually_lt_const

theorem Filter.Tendsto.eventually_le_const {l : Filter Œ≥} {f : Œ≥ ‚Üí Œ±} {u v : Œ±} (hv : v < u)
    (h : Tendsto f l (ùìù v)) : ‚àÄ·∂† a in l, f a ‚â§ u :=
  h.eventually <| eventually_le_nhds hv

@[deprecated (since := "2024-11-17")]
alias eventually_le_of_tendsto_lt := Filter.Tendsto.eventually_le_const

protected theorem Dense.exists_lt [NoMinOrder Œ±] {s : Set Œ±} (hs : Dense s) (x : Œ±) :
    ‚àÉ y ‚àà s, y < x :=
  hs.orderDual.exists_gt x

protected theorem Dense.exists_le [NoMinOrder Œ±] {s : Set Œ±} (hs : Dense s) (x : Œ±) :
    ‚àÉ y ‚àà s, y ‚â§ x :=
  hs.orderDual.exists_ge x

theorem Dense.exists_le' {s : Set Œ±} (hs : Dense s) (hbot : ‚àÄ x, IsBot x ‚Üí x ‚àà s) (x : Œ±) :
    ‚àÉ y ‚àà s, y ‚â§ x :=
  hs.orderDual.exists_ge' hbot x

/-!
### Right neighborhoods on a `ClosedIciTopology`

Limits to the right of real functions are defined in terms of neighborhoods to the right,
either open or closed, i.e., members of `ùìù[>] a` and `ùìù[‚â•] a`.
Here we prove that all right-neighborhoods of a point are equal,
and we prove other useful characterizations which require the stronger hypothesis `OrderTopology Œ±`
in another file.
-/

/-!
#### Point excluded
-/


theorem Ioo_mem_nhdsGT_of_mem (H : b ‚àà Ico a c) : Ioo a c ‚àà ùìù[>] b :=
  mem_nhdsWithin.2
    ‚ü®Iio c, isOpen_Iio, H.2, by rw [inter_comm, Ioi_inter_Iio]; exact Ioo_subset_Ioo_left H.1‚ü©

@[deprecated (since := "2024-12-22")] alias Ioo_mem_nhdsWithin_Ioi := Ioo_mem_nhdsGT_of_mem

theorem Ioo_mem_nhdsGT (H : a < b) : Ioo a b ‚àà ùìù[>] a := Ioo_mem_nhdsGT_of_mem ‚ü®le_rfl, H‚ü©

@[deprecated (since := "2024-12-22")] alias Ioo_mem_nhdsWithin_Ioi' := Ioo_mem_nhdsGT

protected theorem CovBy.nhdsGT (h : a ‚ãñ b) : ùìù[>] a = ‚ä• := h.toDual.nhdsLT

@[deprecated (since := "2024-12-22")] alias CovBy.nhdsWithin_Ioi := CovBy.nhdsGT

protected theorem SuccOrder.nhdsGT [SuccOrder Œ±] : ùìù[>] a = ‚ä• := PredOrder.nhdsLT (Œ± := Œ±·µí·µà)

@[deprecated (since := "2024-12-22")] alias SuccOrder.nhdsWithin_Ioi := SuccOrder.nhdsGT

theorem SuccOrder.nhdsLT_eq_nhdsNE [SuccOrder Œ±] (a : Œ±) : ùìù[<] a = ùìù[‚â†] a :=
  PredOrder.nhdsGT_eq_nhdsNE (Œ± := Œ±·µí·µà) a

theorem SuccOrder.nhdsLE_eq_nhds [SuccOrder Œ±] (a : Œ±) : ùìù[‚â§] a = ùìù a :=
  PredOrder.nhdsGE_eq_nhds (Œ± := Œ±·µí·µà) a

theorem Ioc_mem_nhdsGT_of_mem (H : b ‚àà Ico a c) : Ioc a c ‚àà ùìù[>] b :=
  mem_of_superset (Ioo_mem_nhdsGT_of_mem H) Ioo_subset_Ioc_self

@[deprecated (since := "2024-12-22")]
alias Ioc_mem_nhdsWithin_Ioi := Ioc_mem_nhdsGT_of_mem

theorem Ioc_mem_nhdsGT (H : a < b) : Ioc a b ‚àà ùìù[>] a := Ioc_mem_nhdsGT_of_mem ‚ü®le_rfl, H‚ü©

@[deprecated (since := "2024-12-22")] alias Ioc_mem_nhdsWithin_Ioi' := Ioc_mem_nhdsGT

theorem Ico_mem_nhdsGT_of_mem (H : b ‚àà Ico a c) : Ico a c ‚àà ùìù[>] b :=
  mem_of_superset (Ioo_mem_nhdsGT_of_mem H) Ioo_subset_Ico_self

@[deprecated (since := "2024-12-22")] alias Ico_mem_nhdsWithin_Ioi := Ico_mem_nhdsGT_of_mem

theorem Ico_mem_nhdsGT (H : a < b) : Ico a b ‚àà ùìù[>] a := Ico_mem_nhdsGT_of_mem ‚ü®le_rfl, H‚ü©

@[deprecated (since := "2024-12-22")] alias Ico_mem_nhdsWithin_Ioi' := Ico_mem_nhdsGT

theorem Icc_mem_nhdsGT_of_mem (H : b ‚àà Ico a c) : Icc a c ‚àà ùìù[>] b :=
  mem_of_superset (Ioo_mem_nhdsGT_of_mem H) Ioo_subset_Icc_self

@[deprecated (since := "2024-12-22")] alias Icc_mem_nhdsWithin_Ioi := Icc_mem_nhdsGT_of_mem

theorem Icc_mem_nhdsGT (H : a < b) : Icc a b ‚àà ùìù[>] a := Icc_mem_nhdsGT_of_mem ‚ü®le_rfl, H‚ü©

@[deprecated (since := "2024-12-22")] alias Icc_mem_nhdsWithin_Ioi' := Icc_mem_nhdsGT

@[simp]
theorem nhdsWithin_Ioc_eq_nhdsGT (h : a < b) : ùìù[Ioc a b] a = ùìù[>] a :=
  nhdsWithin_inter_of_mem' <| nhdsWithin_le_nhds <| Iic_mem_nhds h

@[deprecated (since := "2024-12-22")]
alias nhdsWithin_Ioc_eq_nhdsWithin_Ioi := nhdsWithin_Ioc_eq_nhdsGT

@[simp]
theorem nhdsWithin_Ioo_eq_nhdsGT (h : a < b) : ùìù[Ioo a b] a = ùìù[>] a :=
  nhdsWithin_inter_of_mem' <| nhdsWithin_le_nhds <| Iio_mem_nhds h

@[deprecated (since := "2024-12-22")]
alias nhdsWithin_Ioo_eq_nhdsWithin_Ioi := nhdsWithin_Ioo_eq_nhdsGT

@[simp]
theorem continuousWithinAt_Ioc_iff_Ioi (h : a < b) :
    ContinuousWithinAt f (Ioc a b) a ‚Üî ContinuousWithinAt f (Ioi a) a := by
  simp only [ContinuousWithinAt, nhdsWithin_Ioc_eq_nhdsGT h]

@[simp]
theorem continuousWithinAt_Ioo_iff_Ioi (h : a < b) :
    ContinuousWithinAt f (Ioo a b) a ‚Üî ContinuousWithinAt f (Ioi a) a := by
  simp only [ContinuousWithinAt, nhdsWithin_Ioo_eq_nhdsGT h]

/-!
### Point included
-/

protected theorem CovBy.nhdsGE (H : a ‚ãñ b) : ùìù[‚â•] a = pure a := H.toDual.nhdsLE

@[deprecated (since := "2024-12-22")] alias CovBy.nhdsWithin_Ici := CovBy.nhdsGE

protected theorem SuccOrder.nhdsGE [SuccOrder Œ±] : ùìù[‚â•] a = pure a :=
  PredOrder.nhdsLE (Œ± := Œ±·µí·µà)

@[deprecated (since := "2024-12-22")]
alias SuccOrder.nhdsWithin_Ici := SuccOrder.nhdsGE

theorem Ico_mem_nhdsGE (H : a < b) : Ico a b ‚àà ùìù[‚â•] a :=
  inter_mem_nhdsWithin _ <| Iio_mem_nhds H

@[deprecated (since := "2024-12-22")] alias Ico_mem_nhdsWithin_Ici' := Ico_mem_nhdsGE

theorem Ico_mem_nhdsGE_of_mem (H : b ‚àà Ico a c) : Ico a c ‚àà ùìù[‚â•] b :=
  mem_of_superset (Ico_mem_nhdsGE H.2) <| Ico_subset_Ico_left H.1

@[deprecated (since := "2024-12-22")]
alias Ico_mem_nhdsWithin_Ici := Ico_mem_nhdsGE_of_mem

theorem Ioo_mem_nhdsGE_of_mem (H : b ‚àà Ioo a c) : Ioo a c ‚àà ùìù[‚â•] b :=
  mem_of_superset (Ico_mem_nhdsGE H.2) <| Ico_subset_Ioo_left H.1

@[deprecated (since := "2024-12-22")]
alias Ioo_mem_nhdsWithin_Ici := Ioo_mem_nhdsGE_of_mem

theorem Ioc_mem_nhdsGE_of_mem (H : b ‚àà Ioo a c) : Ioc a c ‚àà ùìù[‚â•] b :=
  mem_of_superset (Ioo_mem_nhdsGE_of_mem H) Ioo_subset_Ioc_self

@[deprecated (since := "2024-12-22")] alias Ioc_mem_nhdsWithin_Ici := Ioc_mem_nhdsGE_of_mem

theorem Icc_mem_nhdsGE_of_mem (H : b ‚àà Ico a c) : Icc a c ‚àà ùìù[‚â•] b :=
  mem_of_superset (Ico_mem_nhdsGE_of_mem H) Ico_subset_Icc_self

@[deprecated (since := "2024-12-22")]
alias Icc_mem_nhdsWithin_Ici := Icc_mem_nhdsGE_of_mem

theorem Icc_mem_nhdsGE (H : a < b) : Icc a b ‚àà ùìù[‚â•] a := Icc_mem_nhdsGE_of_mem ‚ü®le_rfl, H‚ü©

@[deprecated (since := "2024-12-22")] alias Icc_mem_nhdsWithin_Ici' := Icc_mem_nhdsGE

@[simp]
theorem nhdsWithin_Icc_eq_nhdsGE (h : a < b) : ùìù[Icc a b] a = ùìù[‚â•] a :=
  nhdsWithin_inter_of_mem' <| nhdsWithin_le_nhds <| Iic_mem_nhds h

@[deprecated (since := "2024-12-22")]
alias nhdsWithin_Icc_eq_nhdsWithin_Ici := nhdsWithin_Icc_eq_nhdsGE

@[simp]
theorem nhdsWithin_Ico_eq_nhdsGE (h : a < b) : ùìù[Ico a b] a = ùìù[‚â•] a :=
  nhdsWithin_inter_of_mem' <| nhdsWithin_le_nhds <| Iio_mem_nhds h

@[deprecated (since := "2024-12-22")]
alias nhdsWithin_Ico_eq_nhdsWithin_Ici := nhdsWithin_Ico_eq_nhdsGE

@[simp]
theorem continuousWithinAt_Icc_iff_Ici (h : a < b) :
    ContinuousWithinAt f (Icc a b) a ‚Üî ContinuousWithinAt f (Ici a) a := by
  simp only [ContinuousWithinAt, nhdsWithin_Icc_eq_nhdsGE h]

@[simp]
theorem continuousWithinAt_Ico_iff_Ici (h : a < b) :
    ContinuousWithinAt f (Ico a b) a ‚Üî ContinuousWithinAt f (Ici a) a := by
  simp only [ContinuousWithinAt, nhdsWithin_Ico_eq_nhdsGE h]

end LinearOrder

end ClosedIciTopology

section OrderClosedTopology

section Preorder

variable [TopologicalSpace Œ±] [Preorder Œ±] [t : OrderClosedTopology Œ±]

namespace Subtype

-- todo: add `OrderEmbedding.orderClosedTopology`
instance {p : Œ± ‚Üí Prop} : OrderClosedTopology (Subtype p) :=
  have this : Continuous fun p : Subtype p √ó Subtype p => ((p.fst : Œ±), (p.snd : Œ±)) :=
    continuous_subtype_val.prodMap continuous_subtype_val
  OrderClosedTopology.mk (t.isClosed_le'.preimage this)

end Subtype

theorem isClosed_le_prod : IsClosed { p : Œ± √ó Œ± | p.1 ‚â§ p.2 } :=
  t.isClosed_le'

theorem isClosed_le [TopologicalSpace Œ≤] {f g : Œ≤ ‚Üí Œ±} (hf : Continuous f) (hg : Continuous g) :
    IsClosed { b | f b ‚â§ g b } :=
  continuous_iff_isClosed.mp (hf.prodMk hg) _ isClosed_le_prod

instance : ClosedIicTopology Œ± where
  isClosed_Iic _ := isClosed_le continuous_id continuous_const

instance : ClosedIciTopology Œ± where
  isClosed_Ici _ := isClosed_le continuous_const continuous_id

instance : OrderClosedTopology Œ±·µí·µà :=
  ‚ü®(OrderClosedTopology.isClosed_le' (Œ± := Œ±)).preimage continuous_swap‚ü©

theorem isClosed_Icc {a b : Œ±} : IsClosed (Icc a b) :=
  IsClosed.inter isClosed_Ici isClosed_Iic

@[simp]
theorem closure_Icc (a b : Œ±) : closure (Icc a b) = Icc a b :=
  isClosed_Icc.closure_eq

theorem le_of_tendsto_of_tendsto_of_frequently {f g : Œ≤ ‚Üí Œ±} {b : Filter Œ≤} {a‚ÇÅ a‚ÇÇ : Œ±}
    (hf : Tendsto f b (ùìù a‚ÇÅ)) (hg : Tendsto g b (ùìù a‚ÇÇ)) (h : ‚àÉ·∂† x in b, f x ‚â§ g x) : a‚ÇÅ ‚â§ a‚ÇÇ :=
  t.isClosed_le'.mem_of_frequently_of_tendsto h (hf.prodMk_nhds hg)

theorem le_of_tendsto_of_tendsto {f g : Œ≤ ‚Üí Œ±} {b : Filter Œ≤} {a‚ÇÅ a‚ÇÇ : Œ±} [NeBot b]
    (hf : Tendsto f b (ùìù a‚ÇÅ)) (hg : Tendsto g b (ùìù a‚ÇÇ)) (h : f ‚â§·∂†[b] g) : a‚ÇÅ ‚â§ a‚ÇÇ :=
  le_of_tendsto_of_tendsto_of_frequently hf hg <| Eventually.frequently h

alias tendsto_le_of_eventuallyLE := le_of_tendsto_of_tendsto

theorem le_of_tendsto_of_tendsto' {f g : Œ≤ ‚Üí Œ±} {b : Filter Œ≤} {a‚ÇÅ a‚ÇÇ : Œ±} [NeBot b]
    (hf : Tendsto f b (ùìù a‚ÇÅ)) (hg : Tendsto g b (ùìù a‚ÇÇ)) (h : ‚àÄ x, f x ‚â§ g x) : a‚ÇÅ ‚â§ a‚ÇÇ :=
  le_of_tendsto_of_tendsto hf hg (Eventually.of_forall h)

@[simp]
theorem closure_le_eq [TopologicalSpace Œ≤] {f g : Œ≤ ‚Üí Œ±} (hf : Continuous f) (hg : Continuous g) :
    closure { b | f b ‚â§ g b } = { b | f b ‚â§ g b } :=
  (isClosed_le hf hg).closure_eq

theorem closure_lt_subset_le [TopologicalSpace Œ≤] {f g : Œ≤ ‚Üí Œ±} (hf : Continuous f)
    (hg : Continuous g) : closure { b | f b < g b } ‚äÜ { b | f b ‚â§ g b } :=
  (closure_minimal fun _ => le_of_lt) <| isClosed_le hf hg

theorem ContinuousWithinAt.closure_le [TopologicalSpace Œ≤] {f g : Œ≤ ‚Üí Œ±} {s : Set Œ≤} {x : Œ≤}
    (hx : x ‚àà closure s) (hf : ContinuousWithinAt f s x) (hg : ContinuousWithinAt g s x)
    (h : ‚àÄ y ‚àà s, f y ‚â§ g y) : f x ‚â§ g x :=
  show (f x, g x) ‚àà { p : Œ± √ó Œ± | p.1 ‚â§ p.2 } from
    OrderClosedTopology.isClosed_le'.closure_subset ((hf.prodMk hg).mem_closure hx h)

/-- If `s` is a closed set and two functions `f` and `g` are continuous on `s`,
then the set `{x ‚àà s | f x ‚â§ g x}` is a closed set. -/
theorem IsClosed.isClosed_le [TopologicalSpace Œ≤] {f g : Œ≤ ‚Üí Œ±} {s : Set Œ≤} (hs : IsClosed s)
    (hf : ContinuousOn f s) (hg : ContinuousOn g s) : IsClosed ({ x ‚àà s | f x ‚â§ g x }) :=
  (hf.prodMk hg).preimage_isClosed_of_isClosed hs OrderClosedTopology.isClosed_le'

theorem le_on_closure [TopologicalSpace Œ≤] {f g : Œ≤ ‚Üí Œ±} {s : Set Œ≤} (h : ‚àÄ x ‚àà s, f x ‚â§ g x)
    (hf : ContinuousOn f (closure s)) (hg : ContinuousOn g (closure s)) ‚¶Éx‚¶Ñ (hx : x ‚àà closure s) :
    f x ‚â§ g x :=
  have : s ‚äÜ { y ‚àà closure s | f y ‚â§ g y } := fun y hy => ‚ü®subset_closure hy, h y hy‚ü©
  (closure_minimal this (isClosed_closure.isClosed_le hf hg) hx).2

theorem IsClosed.epigraph [TopologicalSpace Œ≤] {f : Œ≤ ‚Üí Œ±} {s : Set Œ≤} (hs : IsClosed s)
    (hf : ContinuousOn f s) : IsClosed { p : Œ≤ √ó Œ± | p.1 ‚àà s ‚àß f p.1 ‚â§ p.2 } :=
  (hs.preimage continuous_fst).isClosed_le (hf.comp continuousOn_fst Subset.rfl) continuousOn_snd

theorem IsClosed.hypograph [TopologicalSpace Œ≤] {f : Œ≤ ‚Üí Œ±} {s : Set Œ≤} (hs : IsClosed s)
    (hf : ContinuousOn f s) : IsClosed { p : Œ≤ √ó Œ± | p.1 ‚àà s ‚àß p.2 ‚â§ f p.1 } :=
  (hs.preimage continuous_fst).isClosed_le continuousOn_snd (hf.comp continuousOn_fst Subset.rfl)

/-- The set of monotone functions on a set is closed. -/
theorem isClosed_monotoneOn [Preorder Œ≤] {s : Set Œ≤} : IsClosed {f : Œ≤ ‚Üí Œ± | MonotoneOn f s} := by
  simp only [isClosed_iff_clusterPt, clusterPt_principal_iff_frequently]
  intro g hg a ha b hb hab
  have hmain (x) : Tendsto (fun f' ‚Ü¶ f' x) (ùìù g) (ùìù (g x)) := continuousAt_apply x _
  exact le_of_tendsto_of_tendsto_of_frequently (hmain a) (hmain b) (hg.mono fun g h ‚Ü¶ h ha hb hab)

/-- The set of monotone functions is closed. -/
theorem isClosed_monotone [Preorder Œ≤] : IsClosed {f : Œ≤ ‚Üí Œ± | Monotone f} := by
  simp_rw [‚Üê monotoneOn_univ]
  exact isClosed_monotoneOn

/-- The set of antitone functions on a set is closed. -/
theorem isClosed_antitoneOn [Preorder Œ≤] {s : Set Œ≤} : IsClosed {f : Œ≤ ‚Üí Œ± | AntitoneOn f s} :=
  isClosed_monotoneOn (Œ± := Œ±·µí·µà) (Œ≤ := Œ≤)

/-- The set of antitone functions is closed. -/
theorem isClosed_antitone [Preorder Œ≤] : IsClosed {f : Œ≤ ‚Üí Œ± | Antitone f} :=
  isClosed_monotone (Œ± := Œ±·µí·µà) (Œ≤ := Œ≤)

end Preorder

section PartialOrder

variable [TopologicalSpace Œ±] [PartialOrder Œ±] [t : OrderClosedTopology Œ±]

-- see Note [lower instance priority]
instance (priority := 90) OrderClosedTopology.to_t2Space : T2Space Œ± :=
  t2_iff_isClosed_diagonal.2 <| by
    simpa only [diagonal, le_antisymm_iff] using
      t.isClosed_le'.inter (isClosed_le continuous_snd continuous_fst)

end PartialOrder

section LinearOrder

variable [TopologicalSpace Œ±] [LinearOrder Œ±] [OrderClosedTopology Œ±]

theorem isOpen_lt [TopologicalSpace Œ≤] {f g : Œ≤ ‚Üí Œ±} (hf : Continuous f) (hg : Continuous g) :
    IsOpen { b | f b < g b } := by
  simpa only [lt_iff_not_ge] using (isClosed_le hg hf).isOpen_compl

theorem isOpen_lt_prod : IsOpen { p : Œ± √ó Œ± | p.1 < p.2 } :=
  isOpen_lt continuous_fst continuous_snd

variable {a b : Œ±}

theorem isOpen_Ioo : IsOpen (Ioo a b) :=
  IsOpen.inter isOpen_Ioi isOpen_Iio

@[simp]
theorem interior_Ioo : interior (Ioo a b) = Ioo a b :=
  isOpen_Ioo.interior_eq

theorem Ioo_subset_closure_interior : Ioo a b ‚äÜ closure (interior (Ioo a b)) := by
  simp only [interior_Ioo, subset_closure]

theorem Ioo_mem_nhds {a b x : Œ±} (ha : a < x) (hb : x < b) : Ioo a b ‚àà ùìù x :=
  IsOpen.mem_nhds isOpen_Ioo ‚ü®ha, hb‚ü©

theorem Ioc_mem_nhds {a b x : Œ±} (ha : a < x) (hb : x < b) : Ioc a b ‚àà ùìù x :=
  mem_of_superset (Ioo_mem_nhds ha hb) Ioo_subset_Ioc_self

theorem Ico_mem_nhds {a b x : Œ±} (ha : a < x) (hb : x < b) : Ico a b ‚àà ùìù x :=
  mem_of_superset (Ioo_mem_nhds ha hb) Ioo_subset_Ico_self

theorem Icc_mem_nhds {a b x : Œ±} (ha : a < x) (hb : x < b) : Icc a b ‚àà ùìù x :=
  mem_of_superset (Ioo_mem_nhds ha hb) Ioo_subset_Icc_self

/-- The only order closed topology on a linear order which is a `PredOrder` and a `SuccOrder`
is the discrete topology.

This theorem is not an instance,
because it causes searches for `PredOrder` and `SuccOrder` with their `Preorder` arguments
and very rarely matches. -/
theorem DiscreteTopology.of_predOrder_succOrder [PredOrder Œ±] [SuccOrder Œ±] :
    DiscreteTopology Œ± := by
  refine discreteTopology_iff_nhds.mpr fun a ‚Ü¶ ?_
  rw [‚Üê nhdsWithin_univ, ‚Üê Iic_union_Ioi, nhdsWithin_union, PredOrder.nhdsLE, SuccOrder.nhdsGT,
    sup_bot_eq]

end LinearOrder

section LinearOrder

variable [TopologicalSpace Œ±] [LinearOrder Œ±] [OrderClosedTopology Œ±] {f g : Œ≤ ‚Üí Œ±}

section

variable [TopologicalSpace Œ≤]

theorem lt_subset_interior_le (hf : Continuous f) (hg : Continuous g) :
    { b | f b < g b } ‚äÜ interior { b | f b ‚â§ g b } :=
  (interior_maximal fun _ => le_of_lt) <| isOpen_lt hf hg

theorem frontier_le_subset_eq (hf : Continuous f) (hg : Continuous g) :
    frontier { b | f b ‚â§ g b } ‚äÜ { b | f b = g b } := by
  rw [frontier_eq_closure_inter_closure, closure_le_eq hf hg]
  rintro b ‚ü®hb‚ÇÅ, hb‚ÇÇ‚ü©
  refine le_antisymm hb‚ÇÅ (closure_lt_subset_le hg hf ?_)
  convert hb‚ÇÇ using 2; simp only [not_le.symm]; rfl

theorem frontier_Iic_subset (a : Œ±) : frontier (Iic a) ‚äÜ {a} :=
  frontier_le_subset_eq (@continuous_id Œ± _) continuous_const

theorem frontier_Ici_subset (a : Œ±) : frontier (Ici a) ‚äÜ {a} :=
  frontier_Iic_subset (Œ± := Œ±·µí·µà) _

theorem frontier_lt_subset_eq (hf : Continuous f) (hg : Continuous g) :
    frontier { b | f b < g b } ‚äÜ { b | f b = g b } := by
  simpa only [‚Üê not_lt, ‚Üê compl_setOf, frontier_compl, eq_comm] using frontier_le_subset_eq hg hf

theorem continuous_if_le [TopologicalSpace Œ≥] [‚àÄ x, Decidable (f x ‚â§ g x)] {f' g' : Œ≤ ‚Üí Œ≥}
    (hf : Continuous f) (hg : Continuous g) (hf' : ContinuousOn f' { x | f x ‚â§ g x })
    (hg' : ContinuousOn g' { x | g x ‚â§ f x }) (hfg : ‚àÄ x, f x = g x ‚Üí f' x = g' x) :
    Continuous fun x => if f x ‚â§ g x then f' x else g' x := by
  refine continuous_if (fun a ha => hfg _ (frontier_le_subset_eq hf hg ha)) ?_ (hg'.mono ?_)
  ¬∑ rwa [(isClosed_le hf hg).closure_eq]
  ¬∑ simp only [not_le]
    exact closure_lt_subset_le hg hf

theorem Continuous.if_le [TopologicalSpace Œ≥] [‚àÄ x, Decidable (f x ‚â§ g x)] {f' g' : Œ≤ ‚Üí Œ≥}
    (hf' : Continuous f') (hg' : Continuous g') (hf : Continuous f) (hg : Continuous g)
    (hfg : ‚àÄ x, f x = g x ‚Üí f' x = g' x) : Continuous fun x => if f x ‚â§ g x then f' x else g' x :=
  continuous_if_le hf hg hf'.continuousOn hg'.continuousOn hfg

theorem Filter.Tendsto.eventually_lt {l : Filter Œ≥} {f g : Œ≥ ‚Üí Œ±} {y z : Œ±} (hf : Tendsto f l (ùìù y))
    (hg : Tendsto g l (ùìù z)) (hyz : y < z) : ‚àÄ·∂† x in l, f x < g x :=
  let ‚ü®_a, ha, _b, hb, h‚ü© := hyz.exists_disjoint_Iio_Ioi
  (hg.eventually (Ioi_mem_nhds hb)).mp <| (hf.eventually (Iio_mem_nhds ha)).mono fun _ h‚ÇÅ h‚ÇÇ =>
    h _ h‚ÇÅ _ h‚ÇÇ

nonrec theorem ContinuousAt.eventually_lt {x‚ÇÄ : Œ≤} (hf : ContinuousAt f x‚ÇÄ) (hg : ContinuousAt g x‚ÇÄ)
    (hfg : f x‚ÇÄ < g x‚ÇÄ) : ‚àÄ·∂† x in ùìù x‚ÇÄ, f x < g x :=
  hf.eventually_lt hg hfg

@[continuity, fun_prop]
protected theorem Continuous.min (hf : Continuous f) (hg : Continuous g) :
    Continuous fun b => min (f b) (g b) := by
  simp only [min_def]
  exact hf.if_le hg hf hg fun x => id

@[continuity, fun_prop]
protected theorem Continuous.max (hf : Continuous f) (hg : Continuous g) :
    Continuous fun b => max (f b) (g b) :=
  Continuous.min (Œ± := Œ±·µí·µà) hf hg

end

theorem continuous_min : Continuous fun p : Œ± √ó Œ± => min p.1 p.2 :=
  continuous_fst.min continuous_snd

theorem continuous_max : Continuous fun p : Œ± √ó Œ± => max p.1 p.2 :=
  continuous_fst.max continuous_snd

protected theorem Filter.Tendsto.max {b : Filter Œ≤} {a‚ÇÅ a‚ÇÇ : Œ±} (hf : Tendsto f b (ùìù a‚ÇÅ))
    (hg : Tendsto g b (ùìù a‚ÇÇ)) : Tendsto (fun b => max (f b) (g b)) b (ùìù (max a‚ÇÅ a‚ÇÇ)) :=
  (continuous_max.tendsto (a‚ÇÅ, a‚ÇÇ)).comp (hf.prodMk_nhds hg)

protected theorem Filter.Tendsto.min {b : Filter Œ≤} {a‚ÇÅ a‚ÇÇ : Œ±} (hf : Tendsto f b (ùìù a‚ÇÅ))
    (hg : Tendsto g b (ùìù a‚ÇÇ)) : Tendsto (fun b => min (f b) (g b)) b (ùìù (min a‚ÇÅ a‚ÇÇ)) :=
  (continuous_min.tendsto (a‚ÇÅ, a‚ÇÇ)).comp (hf.prodMk_nhds hg)

protected theorem Filter.Tendsto.max_right {l : Filter Œ≤} {a : Œ±} (h : Tendsto f l (ùìù a)) :
    Tendsto (fun i => max a (f i)) l (ùìù a) := by
  simpa only [sup_idem] using (tendsto_const_nhds (x := a)).max h

protected theorem Filter.Tendsto.max_left {l : Filter Œ≤} {a : Œ±} (h : Tendsto f l (ùìù a)) :
    Tendsto (fun i => max (f i) a) l (ùìù a) := by
  simp_rw [max_comm _ a]
  exact h.max_right

theorem Filter.tendsto_nhds_max_right {l : Filter Œ≤} {a : Œ±} (h : Tendsto f l (ùìù[>] a)) :
    Tendsto (fun i => max a (f i)) l (ùìù[>] a) := by
  obtain ‚ü®h‚ÇÅ : Tendsto f l (ùìù a), h‚ÇÇ : ‚àÄ·∂† i in l, f i ‚àà Ioi a‚ü© := tendsto_nhdsWithin_iff.mp h
  exact tendsto_nhdsWithin_iff.mpr ‚ü®h‚ÇÅ.max_right, h‚ÇÇ.mono fun i hi => lt_max_of_lt_right hi‚ü©

theorem Filter.tendsto_nhds_max_left {l : Filter Œ≤} {a : Œ±} (h : Tendsto f l (ùìù[>] a)) :
    Tendsto (fun i => max (f i) a) l (ùìù[>] a) := by
  simp_rw [max_comm _ a]
  exact Filter.tendsto_nhds_max_right h

theorem Filter.Tendsto.min_right {l : Filter Œ≤} {a : Œ±} (h : Tendsto f l (ùìù a)) :
    Tendsto (fun i => min a (f i)) l (ùìù a) :=
  Filter.Tendsto.max_right (Œ± := Œ±·µí·µà) h

theorem Filter.Tendsto.min_left {l : Filter Œ≤} {a : Œ±} (h : Tendsto f l (ùìù a)) :
    Tendsto (fun i => min (f i) a) l (ùìù a) :=
  Filter.Tendsto.max_left (Œ± := Œ±·µí·µà) h

theorem Filter.tendsto_nhds_min_right {l : Filter Œ≤} {a : Œ±} (h : Tendsto f l (ùìù[<] a)) :
    Tendsto (fun i => min a (f i)) l (ùìù[<] a) :=
  Filter.tendsto_nhds_max_right (Œ± := Œ±·µí·µà) h

theorem Filter.tendsto_nhds_min_left {l : Filter Œ≤} {a : Œ±} (h : Tendsto f l (ùìù[<] a)) :
    Tendsto (fun i => min (f i) a) l (ùìù[<] a) :=
  Filter.tendsto_nhds_max_left (Œ± := Œ±·µí·µà) h

theorem Dense.exists_between [DenselyOrdered Œ±] {s : Set Œ±} (hs : Dense s) {x y : Œ±} (h : x < y) :
    ‚àÉ z ‚àà s, z ‚àà Ioo x y :=
  hs.exists_mem_open isOpen_Ioo (nonempty_Ioo.2 h)

theorem Dense.Ioi_eq_biUnion [DenselyOrdered Œ±] {s : Set Œ±} (hs : Dense s) (x : Œ±) :
    Ioi x = ‚ãÉ y ‚àà s ‚à© Ioi x, Ioi y := by
  refine Subset.antisymm (fun z hz ‚Ü¶ ?_) (iUnion‚ÇÇ_subset fun y hy ‚Ü¶ Ioi_subset_Ioi (le_of_lt hy.2))
  rcases hs.exists_between hz with ‚ü®y, hys, hxy, hyz‚ü©
  exact mem_iUnion‚ÇÇ.2 ‚ü®y, ‚ü®hys, hxy‚ü©, hyz‚ü©

theorem Dense.Iio_eq_biUnion [DenselyOrdered Œ±] {s : Set Œ±} (hs : Dense s) (x : Œ±) :
    Iio x = ‚ãÉ y ‚àà s ‚à© Iio x, Iio y :=
  Dense.Ioi_eq_biUnion (Œ± := Œ±·µí·µà) hs x

end LinearOrder

end OrderClosedTopology

instance [Preorder Œ±] [TopologicalSpace Œ±] [OrderClosedTopology Œ±] [Preorder Œ≤] [TopologicalSpace Œ≤]
    [OrderClosedTopology Œ≤] : OrderClosedTopology (Œ± √ó Œ≤) :=
  ‚ü®(isClosed_le continuous_fst.fst continuous_snd.fst).inter
    (isClosed_le continuous_fst.snd continuous_snd.snd)‚ü©

instance {Œπ : Type*} {Œ± : Œπ ‚Üí Type*} [‚àÄ i, Preorder (Œ± i)] [‚àÄ i, TopologicalSpace (Œ± i)]
    [‚àÄ i, OrderClosedTopology (Œ± i)] : OrderClosedTopology (‚àÄ i, Œ± i) := by
  constructor
  simp only [Pi.le_def, setOf_forall]
  exact isClosed_iInter fun i => isClosed_le (continuous_apply i).fst' (continuous_apply i).snd'

instance Pi.orderClosedTopology' [Preorder Œ≤] [TopologicalSpace Œ≤] [OrderClosedTopology Œ≤] :
    OrderClosedTopology (Œ± ‚Üí Œ≤) :=
  inferInstance
