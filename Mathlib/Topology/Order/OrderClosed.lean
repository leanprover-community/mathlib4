/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Yury Kudryashov
-/
import Mathlib.Topology.Order.LeftRight
import Mathlib.Topology.Separation.Hausdorff

/-!
# Order-closed topologies

In this file we introduce 3 typeclass mixins that relate topology and order structures:

- `ClosedIicTopology` says that all the intervals $(-âˆ, a]$ (formally, `Set.Iic a`)
  are closed sets;
- `ClosedIciTopology` says that all the intervals $[a, +âˆ)$ (formally, `Set.Ici a`)
  are closed sets;
- `OrderClosedTopology` says that the set of points `(x, y)` such that `x â‰¤ y`
  is closed in the product topology.

The last predicate implies the first two.

We prove many basic properties of such topologies.

## Main statements

This file contains the proofs of the following facts.
For exact requirements
(`OrderClosedTopology` vs `ClosedIciTopology` vs `ClosedIicTopology,
`Preorder` vs `PartialOrder` vs `LinearOrder` etc)
see their statements.

### Open / closed sets

* `isOpen_lt` : if `f` and `g` are continuous functions, then `{x | f x < g x}` is open;
* `isOpen_Iio`, `isOpen_Ioi`, `isOpen_Ioo` : open intervals are open;
* `isClosed_le` : if `f` and `g` are continuous functions, then `{x | f x â‰¤ g x}` is closed;
* `isClosed_Iic`, `isClosed_Ici`, `isClosed_Icc` : closed intervals are closed;
* `frontier_le_subset_eq`, `frontier_lt_subset_eq` : frontiers of both `{x | f x â‰¤ g x}`
  and `{x | f x < g x}` are included by `{x | f x = g x}`;

### Convergence and inequalities

* `le_of_tendsto_of_tendsto` : if `f` converges to `a`, `g` converges to `b`, and eventually
  `f x â‰¤ g x`, then `a â‰¤ b`
* `le_of_tendsto`, `ge_of_tendsto` : if `f` converges to `a` and eventually `f x â‰¤ b`
  (resp., `b â‰¤ f x`), then `a â‰¤ b` (resp., `b â‰¤ a`); we also provide primed versions
  that assume the inequalities to hold for all `x`.

### Min, max, `sSup` and `sInf`

* `Continuous.min`, `Continuous.max`: pointwise `min`/`max` of two continuous functions is
  continuous.
* `Tendsto.min`, `Tendsto.max` : if `f` tends to `a` and `g` tends to `b`, then their pointwise
  `min`/`max` tend to `min a b` and `max a b`, respectively.
-/

open Set Filter
open OrderDual (toDual)
open scoped Topology

universe u v w
variable {Î± : Type u} {Î² : Type v} {Î³ : Type w}

/-- If `Î±` is a topological space and a preorder, `ClosedIicTopology Î±` means that `Iic a` is
closed for all `a : Î±`. -/
class ClosedIicTopology (Î± : Type*) [TopologicalSpace Î±] [Preorder Î±] : Prop where
  /-- For any `a`, the set `(-âˆ, a]` is closed. -/
  isClosed_Iic (a : Î±) : IsClosed (Iic a)

/-- If `Î±` is a topological space and a preorder, `ClosedIciTopology Î±` means that `Ici a` is
closed for all `a : Î±`. -/
class ClosedIciTopology (Î± : Type*) [TopologicalSpace Î±] [Preorder Î±] : Prop where
  /-- For any `a`, the set `[a, +âˆ)` is closed. -/
  isClosed_Ici (a : Î±) : IsClosed (Ici a)

/-- A topology on a set which is both a topological space and a preorder is _order-closed_ if the
set of points `(x, y)` with `x â‰¤ y` is closed in the product space. We introduce this as a mixin.
This property is satisfied for the order topology on a linear order, but it can be satisfied more
generally, and suffices to derive many interesting properties relating order and topology. -/
class OrderClosedTopology (Î± : Type*) [TopologicalSpace Î±] [Preorder Î±] : Prop where
  /-- The set `{ (x, y) | x â‰¤ y }` is a closed set. -/
  isClosed_le' : IsClosed { p : Î± Ã— Î± | p.1 â‰¤ p.2 }

instance [TopologicalSpace Î±] [h : FirstCountableTopology Î±] : FirstCountableTopology Î±áµ’áµˆ := h
instance [TopologicalSpace Î±] [h : SecondCountableTopology Î±] : SecondCountableTopology Î±áµ’áµˆ := h

theorem Dense.orderDual [TopologicalSpace Î±] {s : Set Î±} (hs : Dense s) :
    Dense (OrderDual.ofDual â»Â¹' s) :=
  hs

section General
variable [TopologicalSpace Î±] [Preorder Î±] {s : Set Î±}

protected lemma BddAbove.of_closure : BddAbove (closure s) â†’ BddAbove s :=
  BddAbove.mono subset_closure

protected lemma BddBelow.of_closure : BddBelow (closure s) â†’ BddBelow s :=
  BddBelow.mono subset_closure

end General

section ClosedIicTopology

section Preorder

variable [TopologicalSpace Î±] [Preorder Î±] [ClosedIicTopology Î±] {f : Î² â†’ Î±} {a b : Î±} {s : Set Î±}

theorem isClosed_Iic : IsClosed (Iic a) :=
  ClosedIicTopology.isClosed_Iic a

instance : ClosedIciTopology Î±áµ’áµˆ where
  isClosed_Ici _ := isClosed_Iic (Î± := Î±)

@[simp]
theorem closure_Iic (a : Î±) : closure (Iic a) = Iic a :=
  isClosed_Iic.closure_eq

theorem le_of_tendsto_of_frequently {x : Filter Î²} (lim : Tendsto f x (ğ“ a))
    (h : âˆƒá¶  c in x, f c â‰¤ b) : a â‰¤ b :=
  isClosed_Iic.mem_of_frequently_of_tendsto h lim

theorem le_of_tendsto {x : Filter Î²} [NeBot x] (lim : Tendsto f x (ğ“ a))
    (h : âˆ€á¶  c in x, f c â‰¤ b) : a â‰¤ b :=
  isClosed_Iic.mem_of_tendsto lim h

theorem le_of_tendsto' {x : Filter Î²} [NeBot x] (lim : Tendsto f x (ğ“ a))
    (h : âˆ€ c, f c â‰¤ b) : a â‰¤ b :=
  le_of_tendsto lim (Eventually.of_forall h)

@[simp] lemma upperBounds_closure (s : Set Î±) : upperBounds (closure s : Set Î±) = upperBounds s :=
  ext fun a â†¦ by simp_rw [mem_upperBounds_iff_subset_Iic, isClosed_Iic.closure_subset_iff]

@[simp] lemma bddAbove_closure : BddAbove (closure s) â†” BddAbove s := by
  simp_rw [BddAbove, upperBounds_closure]

protected alias âŸ¨_, BddAbove.closureâŸ© := bddAbove_closure

@[simp]
theorem disjoint_nhds_atBot_iff : Disjoint (ğ“ a) atBot â†” Â¬IsBot a := by
  constructor
  Â· intro hd hbot
    rw [hbot.atBot_eq, disjoint_principal_right] at hd
    exact mem_of_mem_nhds hd le_rfl
  Â· simp only [IsBot, not_forall]
    rintro âŸ¨b, hbâŸ©
    refine disjoint_of_disjoint_of_mem disjoint_compl_left ?_ (Iic_mem_atBot b)
    exact isClosed_Iic.isOpen_compl.mem_nhds hb

theorem IsLUB.range_of_tendsto {F : Filter Î²} [F.NeBot] (hle : âˆ€ i, f i â‰¤ a)
    (hlim : Tendsto f F (ğ“ a)) : IsLUB (range f) a :=
  âŸ¨forall_mem_range.mpr hle, fun _c hc â†¦ le_of_tendsto' hlim fun i â†¦ hc <| mem_range_self iâŸ©

end Preorder

section NoBotOrder

variable [Preorder Î±] [NoBotOrder Î±] [TopologicalSpace Î±] [ClosedIicTopology Î±] {a : Î±}
  {l : Filter Î²} [NeBot l] {f : Î² â†’ Î±}

theorem disjoint_nhds_atBot (a : Î±) : Disjoint (ğ“ a) atBot := by simp

@[simp]
theorem inf_nhds_atBot (a : Î±) : ğ“ a âŠ“ atBot = âŠ¥ := (disjoint_nhds_atBot a).eq_bot

theorem not_tendsto_nhds_of_tendsto_atBot (hf : Tendsto f l atBot) (a : Î±) : Â¬Tendsto f l (ğ“ a) :=
  hf.not_tendsto (disjoint_nhds_atBot a).symm

theorem not_tendsto_atBot_of_tendsto_nhds (hf : Tendsto f l (ğ“ a)) : Â¬Tendsto f l atBot :=
  hf.not_tendsto (disjoint_nhds_atBot a)

end NoBotOrder

theorem iSup_eq_of_forall_le_of_tendsto {Î¹ : Type*} {F : Filter Î¹} [Filter.NeBot F]
    [ConditionallyCompleteLattice Î±] [TopologicalSpace Î±] [ClosedIicTopology Î±]
    {a : Î±} {f : Î¹ â†’ Î±} (hle : âˆ€ i, f i â‰¤ a) (hlim : Filter.Tendsto f F (ğ“ a)) :
    â¨† i, f i = a :=
  have := F.nonempty_of_neBot
  (IsLUB.range_of_tendsto hle hlim).ciSup_eq

theorem iUnion_Iic_eq_Iio_of_lt_of_tendsto {Î¹ : Type*} {F : Filter Î¹} [F.NeBot]
    [ConditionallyCompleteLinearOrder Î±] [TopologicalSpace Î±] [ClosedIicTopology Î±]
    {a : Î±} {f : Î¹ â†’ Î±} (hlt : âˆ€ i, f i < a) (hlim : Tendsto f F (ğ“ a)) :
    â‹ƒ i : Î¹, Iic (f i) = Iio a := by
  have obs : a âˆ‰ range f := by
    rw [mem_range]
    rintro âŸ¨i, rflâŸ©
    exact (hlt i).false
  rw [â† biUnion_range, (IsLUB.range_of_tendsto (le_of_lt <| hlt Â·) hlim).biUnion_Iic_eq_Iio obs]

section LinearOrder

variable [TopologicalSpace Î±] [LinearOrder Î±] [ClosedIicTopology Î±] [TopologicalSpace Î²]
  {a b c : Î±} {f : Î± â†’ Î²}

theorem isOpen_Ioi : IsOpen (Ioi a) := by
  rw [â† compl_Iic]
  exact isClosed_Iic.isOpen_compl

@[simp]
theorem interior_Ioi : interior (Ioi a) = Ioi a :=
  isOpen_Ioi.interior_eq

theorem Ioi_mem_nhds (h : a < b) : Ioi a âˆˆ ğ“ b := IsOpen.mem_nhds isOpen_Ioi h

theorem eventually_gt_nhds (hab : b < a) : âˆ€á¶  x in ğ“ a, b < x := Ioi_mem_nhds hab

theorem Ici_mem_nhds (h : a < b) : Ici a âˆˆ ğ“ b :=
  mem_of_superset (Ioi_mem_nhds h) Ioi_subset_Ici_self

theorem eventually_ge_nhds (hab : b < a) : âˆ€á¶  x in ğ“ a, b â‰¤ x := Ici_mem_nhds hab

theorem Filter.Tendsto.eventually_const_lt {l : Filter Î³} {f : Î³ â†’ Î±} {u v : Î±} (hv : u < v)
    (h : Filter.Tendsto f l (ğ“ v)) : âˆ€á¶  a in l, u < f a :=
  h.eventually <| eventually_gt_nhds hv

theorem Filter.Tendsto.eventually_const_le {l : Filter Î³} {f : Î³ â†’ Î±} {u v : Î±} (hv : u < v)
    (h : Tendsto f l (ğ“ v)) : âˆ€á¶  a in l, u â‰¤ f a :=
  h.eventually <| eventually_ge_nhds hv

protected theorem Dense.exists_gt [NoMaxOrder Î±] {s : Set Î±} (hs : Dense s) (x : Î±) :
    âˆƒ y âˆˆ s, x < y :=
  hs.exists_mem_open isOpen_Ioi (exists_gt x)

protected theorem Dense.exists_ge [NoMaxOrder Î±] {s : Set Î±} (hs : Dense s) (x : Î±) :
    âˆƒ y âˆˆ s, x â‰¤ y :=
  (hs.exists_gt x).imp fun _ h â†¦ âŸ¨h.1, h.2.leâŸ©

theorem Dense.exists_ge' {s : Set Î±} (hs : Dense s) (htop : âˆ€ x, IsTop x â†’ x âˆˆ s) (x : Î±) :
    âˆƒ y âˆˆ s, x â‰¤ y := by
  by_cases hx : IsTop x
  Â· exact âŸ¨x, htop x hx, le_rflâŸ©
  Â· simp only [IsTop, not_forall, not_le] at hx
    rcases hs.exists_mem_open isOpen_Ioi hx with âŸ¨y, hys, hy : x < yâŸ©
    exact âŸ¨y, hys, hy.leâŸ©

/-!
### Left neighborhoods on a `ClosedIicTopology`

Limits to the left of real functions are defined in terms of neighborhoods to the left,
either open or closed, i.e., members of `ğ“[<] a` and `ğ“[â‰¤] a`.
Here we prove that all left-neighborhoods of a point are equal,
and we prove other useful characterizations which require the stronger hypothesis `OrderTopology Î±`
in another file.
-/

/-!
#### Point excluded
-/

theorem Ioo_mem_nhdsLT (H : a < b) : Ioo a b âˆˆ ğ“[<] b := by
  simpa only [â† Iio_inter_Ioi] using inter_mem_nhdsWithin _ (Ioi_mem_nhds H)

theorem Ioo_mem_nhdsLT_of_mem (H : b âˆˆ Ioc a c) : Ioo a c âˆˆ ğ“[<] b :=
  mem_of_superset (Ioo_mem_nhdsLT H.1) <| Ioo_subset_Ioo_right H.2

protected theorem CovBy.nhdsLT (h : a â‹– b) : ğ“[<] b = âŠ¥ :=
  empty_mem_iff_bot.mp <| h.Ioo_eq â–¸ Ioo_mem_nhdsLT h.1

protected theorem PredOrder.nhdsLT [PredOrder Î±] : ğ“[<] a = âŠ¥ := by
  if h : IsMin a then simp [h.Iio_eq]
  else exact (Order.pred_covBy_of_not_isMin h).nhdsLT

theorem PredOrder.nhdsGT_eq_nhdsNE [PredOrder Î±] (a : Î±) : ğ“[>] a = ğ“[â‰ ] a := by
  rw [â† nhdsLT_sup_nhdsGT, PredOrder.nhdsLT, bot_sup_eq]

theorem PredOrder.nhdsGE_eq_nhds [PredOrder Î±] (a : Î±) : ğ“[â‰¥] a = ğ“ a := by
  rw [â† nhdsLT_sup_nhdsGE, PredOrder.nhdsLT, bot_sup_eq]

theorem Ico_mem_nhdsLT_of_mem (H : b âˆˆ Ioc a c) : Ico a c âˆˆ ğ“[<] b :=
  mem_of_superset (Ioo_mem_nhdsLT_of_mem H) Ioo_subset_Ico_self

theorem Ico_mem_nhdsLT (H : a < b) : Ico a b âˆˆ ğ“[<] b := Ico_mem_nhdsLT_of_mem âŸ¨H, le_rflâŸ©

theorem Ioc_mem_nhdsLT_of_mem (H : b âˆˆ Ioc a c) : Ioc a c âˆˆ ğ“[<] b :=
  mem_of_superset (Ioo_mem_nhdsLT_of_mem H) Ioo_subset_Ioc_self

theorem Ioc_mem_nhdsLT (H : a < b) : Ioc a b âˆˆ ğ“[<] b := Ioc_mem_nhdsLT_of_mem âŸ¨H, le_rflâŸ©

theorem Icc_mem_nhdsLT_of_mem (H : b âˆˆ Ioc a c) : Icc a c âˆˆ ğ“[<] b :=
  mem_of_superset (Ioo_mem_nhdsLT_of_mem H) Ioo_subset_Icc_self

theorem Icc_mem_nhdsLT (H : a < b) : Icc a b âˆˆ ğ“[<] b := Icc_mem_nhdsLT_of_mem âŸ¨H, le_rflâŸ©

@[simp]
theorem nhdsWithin_Ico_eq_nhdsLT (h : a < b) : ğ“[Ico a b] b = ğ“[<] b :=
  nhdsWithin_inter_of_mem <| nhdsWithin_le_nhds <| Ici_mem_nhds h

@[simp]
theorem nhdsWithin_Ioo_eq_nhdsLT (h : a < b) : ğ“[Ioo a b] b = ğ“[<] b :=
  nhdsWithin_inter_of_mem <| nhdsWithin_le_nhds <| Ioi_mem_nhds h

@[simp]
theorem continuousWithinAt_Ico_iff_Iio (h : a < b) :
    ContinuousWithinAt f (Ico a b) b â†” ContinuousWithinAt f (Iio b) b := by
  simp only [ContinuousWithinAt, nhdsWithin_Ico_eq_nhdsLT h]

@[simp]
theorem continuousWithinAt_Ioo_iff_Iio (h : a < b) :
    ContinuousWithinAt f (Ioo a b) b â†” ContinuousWithinAt f (Iio b) b := by
  simp only [ContinuousWithinAt, nhdsWithin_Ioo_eq_nhdsLT h]

/-!
#### Point included
-/

protected theorem CovBy.nhdsLE (H : a â‹– b) : ğ“[â‰¤] b = pure b := by
  rw [â† Iio_insert, nhdsWithin_insert, H.nhdsLT, sup_bot_eq]

protected theorem PredOrder.nhdsLE [PredOrder Î±] : ğ“[â‰¤] b = pure b := by
  rw [â† Iio_insert, nhdsWithin_insert, PredOrder.nhdsLT, sup_bot_eq]

theorem Ioc_mem_nhdsLE (H : a < b) : Ioc a b âˆˆ ğ“[â‰¤] b :=
  inter_mem (nhdsWithin_le_nhds <| Ioi_mem_nhds H) self_mem_nhdsWithin

theorem Ioo_mem_nhdsLE_of_mem (H : b âˆˆ Ioo a c) : Ioo a c âˆˆ ğ“[â‰¤] b :=
  mem_of_superset (Ioc_mem_nhdsLE H.1) <| Ioc_subset_Ioo_right H.2

theorem Ico_mem_nhdsLE_of_mem (H : b âˆˆ Ioo a c) : Ico a c âˆˆ ğ“[â‰¤] b :=
  mem_of_superset (Ioo_mem_nhdsLE_of_mem H) Ioo_subset_Ico_self

theorem Ioc_mem_nhdsLE_of_mem (H : b âˆˆ Ioc a c) : Ioc a c âˆˆ ğ“[â‰¤] b :=
  mem_of_superset (Ioc_mem_nhdsLE H.1) <| Ioc_subset_Ioc_right H.2

theorem Icc_mem_nhdsLE_of_mem (H : b âˆˆ Ioc a c) : Icc a c âˆˆ ğ“[â‰¤] b :=
  mem_of_superset (Ioc_mem_nhdsLE_of_mem H) Ioc_subset_Icc_self

theorem Icc_mem_nhdsLE (H : a < b) : Icc a b âˆˆ ğ“[â‰¤] b := Icc_mem_nhdsLE_of_mem âŸ¨H, le_rflâŸ©

@[simp]
theorem nhdsWithin_Icc_eq_nhdsLE (h : a < b) : ğ“[Icc a b] b = ğ“[â‰¤] b :=
  nhdsWithin_inter_of_mem <| nhdsWithin_le_nhds <| Ici_mem_nhds h

@[simp]
theorem nhdsWithin_Ioc_eq_nhdsLE (h : a < b) : ğ“[Ioc a b] b = ğ“[â‰¤] b :=
  nhdsWithin_inter_of_mem <| nhdsWithin_le_nhds <| Ioi_mem_nhds h

@[simp]
theorem continuousWithinAt_Icc_iff_Iic (h : a < b) :
    ContinuousWithinAt f (Icc a b) b â†” ContinuousWithinAt f (Iic b) b := by
  simp only [ContinuousWithinAt, nhdsWithin_Icc_eq_nhdsLE h]

@[simp]
theorem continuousWithinAt_Ioc_iff_Iic (h : a < b) :
    ContinuousWithinAt f (Ioc a b) b â†” ContinuousWithinAt f (Iic b) b := by
  simp only [ContinuousWithinAt, nhdsWithin_Ioc_eq_nhdsLE h]

end LinearOrder

end ClosedIicTopology

section ClosedIciTopology

section Preorder

variable [TopologicalSpace Î±] [Preorder Î±] [ClosedIciTopology Î±] {f : Î² â†’ Î±} {a b : Î±} {s : Set Î±}

theorem isClosed_Ici {a : Î±} : IsClosed (Ici a) :=
  ClosedIciTopology.isClosed_Ici a

instance : ClosedIicTopology Î±áµ’áµˆ where
  isClosed_Iic _ := isClosed_Ici (Î± := Î±)

@[simp]
theorem closure_Ici (a : Î±) : closure (Ici a) = Ici a :=
  isClosed_Ici.closure_eq

lemma ge_of_tendsto_of_frequently {x : Filter Î²} (lim : Tendsto f x (ğ“ a))
    (h : âˆƒá¶  c in x, b â‰¤ f c) : b â‰¤ a :=
  isClosed_Ici.mem_of_frequently_of_tendsto h lim

theorem ge_of_tendsto {x : Filter Î²} [NeBot x] (lim : Tendsto f x (ğ“ a))
    (h : âˆ€á¶  c in x, b â‰¤ f c) : b â‰¤ a :=
  isClosed_Ici.mem_of_tendsto lim h

theorem ge_of_tendsto' {x : Filter Î²} [NeBot x] (lim : Tendsto f x (ğ“ a))
    (h : âˆ€ c, b â‰¤ f c) : b â‰¤ a :=
  ge_of_tendsto lim (Eventually.of_forall h)

@[simp] lemma lowerBounds_closure (s : Set Î±) : lowerBounds (closure s : Set Î±) = lowerBounds s :=
  ext fun a â†¦ by simp_rw [mem_lowerBounds_iff_subset_Ici, isClosed_Ici.closure_subset_iff]

@[simp] lemma bddBelow_closure : BddBelow (closure s) â†” BddBelow s := by
  simp_rw [BddBelow, lowerBounds_closure]

protected alias âŸ¨_, BddBelow.closureâŸ© := bddBelow_closure

@[simp]
theorem disjoint_nhds_atTop_iff : Disjoint (ğ“ a) atTop â†” Â¬IsTop a :=
  disjoint_nhds_atBot_iff (Î± := Î±áµ’áµˆ)

theorem IsGLB.range_of_tendsto {F : Filter Î²} [F.NeBot] (hle : âˆ€ i, a â‰¤ f i)
    (hlim : Tendsto f F (ğ“ a)) : IsGLB (range f) a :=
  IsLUB.range_of_tendsto (Î± := Î±áµ’áµˆ) hle hlim

end Preorder

section NoTopOrder

variable [Preorder Î±] [NoTopOrder Î±] [TopologicalSpace Î±] [ClosedIciTopology Î±] {a : Î±}
  {l : Filter Î²} [NeBot l] {f : Î² â†’ Î±}

theorem disjoint_nhds_atTop (a : Î±) : Disjoint (ğ“ a) atTop := disjoint_nhds_atBot (toDual a)

@[simp]
theorem inf_nhds_atTop (a : Î±) : ğ“ a âŠ“ atTop = âŠ¥ := (disjoint_nhds_atTop a).eq_bot

theorem not_tendsto_nhds_of_tendsto_atTop (hf : Tendsto f l atTop) (a : Î±) : Â¬Tendsto f l (ğ“ a) :=
  hf.not_tendsto (disjoint_nhds_atTop a).symm

theorem not_tendsto_atTop_of_tendsto_nhds (hf : Tendsto f l (ğ“ a)) : Â¬Tendsto f l atTop :=
  hf.not_tendsto (disjoint_nhds_atTop a)

end NoTopOrder

theorem iInf_eq_of_forall_le_of_tendsto {Î¹ : Type*} {F : Filter Î¹} [F.NeBot]
    [ConditionallyCompleteLattice Î±] [TopologicalSpace Î±] [ClosedIciTopology Î±]
    {a : Î±} {f : Î¹ â†’ Î±} (hle : âˆ€ i, a â‰¤ f i) (hlim : Tendsto f F (ğ“ a)) :
    â¨… i, f i = a :=
  iSup_eq_of_forall_le_of_tendsto (Î± := Î±áµ’áµˆ) hle hlim

theorem iUnion_Ici_eq_Ioi_of_lt_of_tendsto {Î¹ : Type*} {F : Filter Î¹} [F.NeBot]
    [ConditionallyCompleteLinearOrder Î±] [TopologicalSpace Î±] [ClosedIciTopology Î±]
    {a : Î±} {f : Î¹ â†’ Î±} (hlt : âˆ€ i, a < f i) (hlim : Tendsto f F (ğ“ a)) :
    â‹ƒ i : Î¹, Ici (f i) = Ioi a :=
  iUnion_Iic_eq_Iio_of_lt_of_tendsto (Î± := Î±áµ’áµˆ) hlt hlim

section LinearOrder

variable [TopologicalSpace Î±] [LinearOrder Î±] [ClosedIciTopology Î±] [TopologicalSpace Î²]
  {a b c : Î±} {f : Î± â†’ Î²}

theorem isOpen_Iio : IsOpen (Iio a) := isOpen_Ioi (Î± := Î±áµ’áµˆ)

@[simp] theorem interior_Iio : interior (Iio a) = Iio a := isOpen_Iio.interior_eq

theorem Iio_mem_nhds (h : a < b) : Iio b âˆˆ ğ“ a := isOpen_Iio.mem_nhds h

theorem eventually_lt_nhds (hab : a < b) : âˆ€á¶  x in ğ“ a, x < b := Iio_mem_nhds hab

theorem Iic_mem_nhds (h : a < b) : Iic b âˆˆ ğ“ a :=
  mem_of_superset (Iio_mem_nhds h) Iio_subset_Iic_self

theorem eventually_le_nhds (hab : a < b) : âˆ€á¶  x in ğ“ a, x â‰¤ b := Iic_mem_nhds hab

theorem Filter.Tendsto.eventually_lt_const {l : Filter Î³} {f : Î³ â†’ Î±} {u v : Î±} (hv : v < u)
    (h : Filter.Tendsto f l (ğ“ v)) : âˆ€á¶  a in l, f a < u :=
  h.eventually <| eventually_lt_nhds hv

theorem Filter.Tendsto.eventually_le_const {l : Filter Î³} {f : Î³ â†’ Î±} {u v : Î±} (hv : v < u)
    (h : Tendsto f l (ğ“ v)) : âˆ€á¶  a in l, f a â‰¤ u :=
  h.eventually <| eventually_le_nhds hv

protected theorem Dense.exists_lt [NoMinOrder Î±] {s : Set Î±} (hs : Dense s) (x : Î±) :
    âˆƒ y âˆˆ s, y < x :=
  hs.orderDual.exists_gt x

protected theorem Dense.exists_le [NoMinOrder Î±] {s : Set Î±} (hs : Dense s) (x : Î±) :
    âˆƒ y âˆˆ s, y â‰¤ x :=
  hs.orderDual.exists_ge x

theorem Dense.exists_le' {s : Set Î±} (hs : Dense s) (hbot : âˆ€ x, IsBot x â†’ x âˆˆ s) (x : Î±) :
    âˆƒ y âˆˆ s, y â‰¤ x :=
  hs.orderDual.exists_ge' hbot x

/-!
### Right neighborhoods on a `ClosedIciTopology`

Limits to the right of real functions are defined in terms of neighborhoods to the right,
either open or closed, i.e., members of `ğ“[>] a` and `ğ“[â‰¥] a`.
Here we prove that all right-neighborhoods of a point are equal,
and we prove other useful characterizations which require the stronger hypothesis `OrderTopology Î±`
in another file.
-/

/-!
#### Point excluded
-/


theorem Ioo_mem_nhdsGT_of_mem (H : b âˆˆ Ico a c) : Ioo a c âˆˆ ğ“[>] b :=
  mem_nhdsWithin.2
    âŸ¨Iio c, isOpen_Iio, H.2, by rw [inter_comm, Ioi_inter_Iio]; exact Ioo_subset_Ioo_left H.1âŸ©

theorem Ioo_mem_nhdsGT (H : a < b) : Ioo a b âˆˆ ğ“[>] a := Ioo_mem_nhdsGT_of_mem âŸ¨le_rfl, HâŸ©

protected theorem CovBy.nhdsGT (h : a â‹– b) : ğ“[>] a = âŠ¥ := h.toDual.nhdsLT

protected theorem SuccOrder.nhdsGT [SuccOrder Î±] : ğ“[>] a = âŠ¥ := PredOrder.nhdsLT (Î± := Î±áµ’áµˆ)

theorem SuccOrder.nhdsLT_eq_nhdsNE [SuccOrder Î±] (a : Î±) : ğ“[<] a = ğ“[â‰ ] a :=
  PredOrder.nhdsGT_eq_nhdsNE (Î± := Î±áµ’áµˆ) a

theorem SuccOrder.nhdsLE_eq_nhds [SuccOrder Î±] (a : Î±) : ğ“[â‰¤] a = ğ“ a :=
  PredOrder.nhdsGE_eq_nhds (Î± := Î±áµ’áµˆ) a

theorem Ioc_mem_nhdsGT_of_mem (H : b âˆˆ Ico a c) : Ioc a c âˆˆ ğ“[>] b :=
  mem_of_superset (Ioo_mem_nhdsGT_of_mem H) Ioo_subset_Ioc_self

theorem Ioc_mem_nhdsGT (H : a < b) : Ioc a b âˆˆ ğ“[>] a := Ioc_mem_nhdsGT_of_mem âŸ¨le_rfl, HâŸ©

theorem Ico_mem_nhdsGT_of_mem (H : b âˆˆ Ico a c) : Ico a c âˆˆ ğ“[>] b :=
  mem_of_superset (Ioo_mem_nhdsGT_of_mem H) Ioo_subset_Ico_self

theorem Ico_mem_nhdsGT (H : a < b) : Ico a b âˆˆ ğ“[>] a := Ico_mem_nhdsGT_of_mem âŸ¨le_rfl, HâŸ©

theorem Icc_mem_nhdsGT_of_mem (H : b âˆˆ Ico a c) : Icc a c âˆˆ ğ“[>] b :=
  mem_of_superset (Ioo_mem_nhdsGT_of_mem H) Ioo_subset_Icc_self

theorem Icc_mem_nhdsGT (H : a < b) : Icc a b âˆˆ ğ“[>] a := Icc_mem_nhdsGT_of_mem âŸ¨le_rfl, HâŸ©

@[simp]
theorem nhdsWithin_Ioc_eq_nhdsGT (h : a < b) : ğ“[Ioc a b] a = ğ“[>] a :=
  nhdsWithin_inter_of_mem' <| nhdsWithin_le_nhds <| Iic_mem_nhds h

@[simp]
theorem nhdsWithin_Ioo_eq_nhdsGT (h : a < b) : ğ“[Ioo a b] a = ğ“[>] a :=
  nhdsWithin_inter_of_mem' <| nhdsWithin_le_nhds <| Iio_mem_nhds h

@[simp]
theorem continuousWithinAt_Ioc_iff_Ioi (h : a < b) :
    ContinuousWithinAt f (Ioc a b) a â†” ContinuousWithinAt f (Ioi a) a := by
  simp only [ContinuousWithinAt, nhdsWithin_Ioc_eq_nhdsGT h]

@[simp]
theorem continuousWithinAt_Ioo_iff_Ioi (h : a < b) :
    ContinuousWithinAt f (Ioo a b) a â†” ContinuousWithinAt f (Ioi a) a := by
  simp only [ContinuousWithinAt, nhdsWithin_Ioo_eq_nhdsGT h]

/-!
### Point included
-/

protected theorem CovBy.nhdsGE (H : a â‹– b) : ğ“[â‰¥] a = pure a := H.toDual.nhdsLE

protected theorem SuccOrder.nhdsGE [SuccOrder Î±] : ğ“[â‰¥] a = pure a :=
  PredOrder.nhdsLE (Î± := Î±áµ’áµˆ)

theorem Ico_mem_nhdsGE (H : a < b) : Ico a b âˆˆ ğ“[â‰¥] a :=
  inter_mem_nhdsWithin _ <| Iio_mem_nhds H

theorem Ico_mem_nhdsGE_of_mem (H : b âˆˆ Ico a c) : Ico a c âˆˆ ğ“[â‰¥] b :=
  mem_of_superset (Ico_mem_nhdsGE H.2) <| Ico_subset_Ico_left H.1

theorem Ioo_mem_nhdsGE_of_mem (H : b âˆˆ Ioo a c) : Ioo a c âˆˆ ğ“[â‰¥] b :=
  mem_of_superset (Ico_mem_nhdsGE H.2) <| Ico_subset_Ioo_left H.1

theorem Ioc_mem_nhdsGE_of_mem (H : b âˆˆ Ioo a c) : Ioc a c âˆˆ ğ“[â‰¥] b :=
  mem_of_superset (Ioo_mem_nhdsGE_of_mem H) Ioo_subset_Ioc_self

theorem Icc_mem_nhdsGE_of_mem (H : b âˆˆ Ico a c) : Icc a c âˆˆ ğ“[â‰¥] b :=
  mem_of_superset (Ico_mem_nhdsGE_of_mem H) Ico_subset_Icc_self

theorem Icc_mem_nhdsGE (H : a < b) : Icc a b âˆˆ ğ“[â‰¥] a := Icc_mem_nhdsGE_of_mem âŸ¨le_rfl, HâŸ©

@[simp]
theorem nhdsWithin_Icc_eq_nhdsGE (h : a < b) : ğ“[Icc a b] a = ğ“[â‰¥] a :=
  nhdsWithin_inter_of_mem' <| nhdsWithin_le_nhds <| Iic_mem_nhds h

@[simp]
theorem nhdsWithin_Ico_eq_nhdsGE (h : a < b) : ğ“[Ico a b] a = ğ“[â‰¥] a :=
  nhdsWithin_inter_of_mem' <| nhdsWithin_le_nhds <| Iio_mem_nhds h

@[simp]
theorem continuousWithinAt_Icc_iff_Ici (h : a < b) :
    ContinuousWithinAt f (Icc a b) a â†” ContinuousWithinAt f (Ici a) a := by
  simp only [ContinuousWithinAt, nhdsWithin_Icc_eq_nhdsGE h]

@[simp]
theorem continuousWithinAt_Ico_iff_Ici (h : a < b) :
    ContinuousWithinAt f (Ico a b) a â†” ContinuousWithinAt f (Ici a) a := by
  simp only [ContinuousWithinAt, nhdsWithin_Ico_eq_nhdsGE h]

end LinearOrder

end ClosedIciTopology

section OrderClosedTopology

section Preorder

variable [TopologicalSpace Î±] [Preorder Î±] [t : OrderClosedTopology Î±]

namespace Subtype

-- todo: add `OrderEmbedding.orderClosedTopology`
instance {p : Î± â†’ Prop} : OrderClosedTopology (Subtype p) :=
  have this : Continuous fun p : Subtype p Ã— Subtype p â†¦ ((p.fst : Î±), (p.snd : Î±)) :=
    continuous_subtype_val.prodMap continuous_subtype_val
  OrderClosedTopology.mk (t.isClosed_le'.preimage this)

end Subtype

theorem isClosed_le_prod : IsClosed { p : Î± Ã— Î± | p.1 â‰¤ p.2 } :=
  t.isClosed_le'

theorem isClosed_le [TopologicalSpace Î²] {f g : Î² â†’ Î±} (hf : Continuous f) (hg : Continuous g) :
    IsClosed { b | f b â‰¤ g b } :=
  continuous_iff_isClosed.mp (hf.prodMk hg) _ isClosed_le_prod

instance : ClosedIicTopology Î± where
  isClosed_Iic _ := isClosed_le continuous_id continuous_const

instance : ClosedIciTopology Î± where
  isClosed_Ici _ := isClosed_le continuous_const continuous_id

instance : OrderClosedTopology Î±áµ’áµˆ :=
  âŸ¨(OrderClosedTopology.isClosed_le' (Î± := Î±)).preimage continuous_swapâŸ©

theorem isClosed_Icc {a b : Î±} : IsClosed (Icc a b) :=
  IsClosed.inter isClosed_Ici isClosed_Iic

@[simp]
theorem closure_Icc (a b : Î±) : closure (Icc a b) = Icc a b :=
  isClosed_Icc.closure_eq

theorem le_of_tendsto_of_tendsto_of_frequently {f g : Î² â†’ Î±} {b : Filter Î²} {aâ‚ aâ‚‚ : Î±}
    (hf : Tendsto f b (ğ“ aâ‚)) (hg : Tendsto g b (ğ“ aâ‚‚)) (h : âˆƒá¶  x in b, f x â‰¤ g x) : aâ‚ â‰¤ aâ‚‚ :=
  t.isClosed_le'.mem_of_frequently_of_tendsto h (hf.prodMk_nhds hg)

theorem le_of_tendsto_of_tendsto {f g : Î² â†’ Î±} {b : Filter Î²} {aâ‚ aâ‚‚ : Î±} [NeBot b]
    (hf : Tendsto f b (ğ“ aâ‚)) (hg : Tendsto g b (ğ“ aâ‚‚)) (h : f â‰¤á¶ [b] g) : aâ‚ â‰¤ aâ‚‚ :=
  le_of_tendsto_of_tendsto_of_frequently hf hg <| Eventually.frequently h

alias tendsto_le_of_eventuallyLE := le_of_tendsto_of_tendsto

theorem le_of_tendsto_of_tendsto' {f g : Î² â†’ Î±} {b : Filter Î²} {aâ‚ aâ‚‚ : Î±} [NeBot b]
    (hf : Tendsto f b (ğ“ aâ‚)) (hg : Tendsto g b (ğ“ aâ‚‚)) (h : âˆ€ x, f x â‰¤ g x) : aâ‚ â‰¤ aâ‚‚ :=
  le_of_tendsto_of_tendsto hf hg (Eventually.of_forall h)

@[simp]
theorem closure_le_eq [TopologicalSpace Î²] {f g : Î² â†’ Î±} (hf : Continuous f) (hg : Continuous g) :
    closure { b | f b â‰¤ g b } = { b | f b â‰¤ g b } :=
  (isClosed_le hf hg).closure_eq

theorem closure_lt_subset_le [TopologicalSpace Î²] {f g : Î² â†’ Î±} (hf : Continuous f)
    (hg : Continuous g) : closure { b | f b < g b } âŠ† { b | f b â‰¤ g b } :=
  (closure_minimal fun _ â†¦ le_of_lt) <| isClosed_le hf hg

theorem ContinuousWithinAt.closure_le [TopologicalSpace Î²] {f g : Î² â†’ Î±} {s : Set Î²} {x : Î²}
    (hx : x âˆˆ closure s) (hf : ContinuousWithinAt f s x) (hg : ContinuousWithinAt g s x)
    (h : âˆ€ y âˆˆ s, f y â‰¤ g y) : f x â‰¤ g x :=
  show (f x, g x) âˆˆ { p : Î± Ã— Î± | p.1 â‰¤ p.2 } from
    OrderClosedTopology.isClosed_le'.closure_subset ((hf.prodMk hg).mem_closure hx h)

/-- If `s` is a closed set and two functions `f` and `g` are continuous on `s`,
then the set `{x âˆˆ s | f x â‰¤ g x}` is a closed set. -/
theorem IsClosed.isClosed_le [TopologicalSpace Î²] {f g : Î² â†’ Î±} {s : Set Î²} (hs : IsClosed s)
    (hf : ContinuousOn f s) (hg : ContinuousOn g s) : IsClosed ({ x âˆˆ s | f x â‰¤ g x }) :=
  (hf.prodMk hg).preimage_isClosed_of_isClosed hs OrderClosedTopology.isClosed_le'

theorem le_on_closure [TopologicalSpace Î²] {f g : Î² â†’ Î±} {s : Set Î²} (h : âˆ€ x âˆˆ s, f x â‰¤ g x)
    (hf : ContinuousOn f (closure s)) (hg : ContinuousOn g (closure s)) â¦ƒxâ¦„ (hx : x âˆˆ closure s) :
    f x â‰¤ g x :=
  have : s âŠ† { y âˆˆ closure s | f y â‰¤ g y } := fun y hy â†¦ âŸ¨subset_closure hy, h y hyâŸ©
  (closure_minimal this (isClosed_closure.isClosed_le hf hg) hx).2

theorem IsClosed.epigraph [TopologicalSpace Î²] {f : Î² â†’ Î±} {s : Set Î²} (hs : IsClosed s)
    (hf : ContinuousOn f s) : IsClosed { p : Î² Ã— Î± | p.1 âˆˆ s âˆ§ f p.1 â‰¤ p.2 } :=
  (hs.preimage continuous_fst).isClosed_le (hf.comp continuousOn_fst Subset.rfl) continuousOn_snd

theorem IsClosed.hypograph [TopologicalSpace Î²] {f : Î² â†’ Î±} {s : Set Î²} (hs : IsClosed s)
    (hf : ContinuousOn f s) : IsClosed { p : Î² Ã— Î± | p.1 âˆˆ s âˆ§ p.2 â‰¤ f p.1 } :=
  (hs.preimage continuous_fst).isClosed_le continuousOn_snd (hf.comp continuousOn_fst Subset.rfl)

/-- The set of monotone functions on a set is closed. -/
theorem isClosed_monotoneOn [Preorder Î²] {s : Set Î²} : IsClosed {f : Î² â†’ Î± | MonotoneOn f s} := by
  simp only [isClosed_iff_clusterPt, clusterPt_principal_iff_frequently]
  intro g hg a ha b hb hab
  have hmain (x) : Tendsto (fun f' â†¦ f' x) (ğ“ g) (ğ“ (g x)) := continuousAt_apply x _
  exact le_of_tendsto_of_tendsto_of_frequently (hmain a) (hmain b) (hg.mono fun g h â†¦ h ha hb hab)

/-- The set of monotone functions is closed. -/
theorem isClosed_monotone [Preorder Î²] : IsClosed {f : Î² â†’ Î± | Monotone f} := by
  simp_rw [â† monotoneOn_univ]
  exact isClosed_monotoneOn

/-- The set of antitone functions on a set is closed. -/
theorem isClosed_antitoneOn [Preorder Î²] {s : Set Î²} : IsClosed {f : Î² â†’ Î± | AntitoneOn f s} :=
  isClosed_monotoneOn (Î± := Î±áµ’áµˆ) (Î² := Î²)

/-- The set of antitone functions is closed. -/
theorem isClosed_antitone [Preorder Î²] : IsClosed {f : Î² â†’ Î± | Antitone f} :=
  isClosed_monotone (Î± := Î±áµ’áµˆ) (Î² := Î²)

end Preorder

section PartialOrder

variable [TopologicalSpace Î±] [PartialOrder Î±] [t : OrderClosedTopology Î±]

-- see Note [lower instance priority]
instance (priority := 90) OrderClosedTopology.to_t2Space : T2Space Î± :=
  t2_iff_isClosed_diagonal.2 <| by
    simpa only [diagonal, le_antisymm_iff] using
      t.isClosed_le'.inter (isClosed_le continuous_snd continuous_fst)

end PartialOrder

section LinearOrder

variable [TopologicalSpace Î±] [LinearOrder Î±] [OrderClosedTopology Î±]

theorem isOpen_lt [TopologicalSpace Î²] {f g : Î² â†’ Î±} (hf : Continuous f) (hg : Continuous g) :
    IsOpen { b | f b < g b } := by
  simpa only [lt_iff_not_ge] using (isClosed_le hg hf).isOpen_compl

theorem isOpen_lt_prod : IsOpen { p : Î± Ã— Î± | p.1 < p.2 } :=
  isOpen_lt continuous_fst continuous_snd

variable {a b : Î±}

theorem isOpen_Ioo : IsOpen (Ioo a b) :=
  IsOpen.inter isOpen_Ioi isOpen_Iio

@[simp]
theorem interior_Ioo : interior (Ioo a b) = Ioo a b :=
  isOpen_Ioo.interior_eq

theorem Ioo_subset_closure_interior : Ioo a b âŠ† closure (interior (Ioo a b)) := by
  simp only [interior_Ioo, subset_closure]

theorem Ioo_mem_nhds {a b x : Î±} (ha : a < x) (hb : x < b) : Ioo a b âˆˆ ğ“ x :=
  IsOpen.mem_nhds isOpen_Ioo âŸ¨ha, hbâŸ©

theorem Ioc_mem_nhds {a b x : Î±} (ha : a < x) (hb : x < b) : Ioc a b âˆˆ ğ“ x :=
  mem_of_superset (Ioo_mem_nhds ha hb) Ioo_subset_Ioc_self

theorem Ico_mem_nhds {a b x : Î±} (ha : a < x) (hb : x < b) : Ico a b âˆˆ ğ“ x :=
  mem_of_superset (Ioo_mem_nhds ha hb) Ioo_subset_Ico_self

theorem Icc_mem_nhds {a b x : Î±} (ha : a < x) (hb : x < b) : Icc a b âˆˆ ğ“ x :=
  mem_of_superset (Ioo_mem_nhds ha hb) Ioo_subset_Icc_self

/-- The only order closed topology on a linear order which is a `PredOrder` and a `SuccOrder`
is the discrete topology.

This theorem is not an instance,
because it causes searches for `PredOrder` and `SuccOrder` with their `Preorder` arguments
and very rarely matches. -/
theorem DiscreteTopology.of_predOrder_succOrder [PredOrder Î±] [SuccOrder Î±] :
    DiscreteTopology Î± := by
  refine discreteTopology_iff_nhds.mpr fun a â†¦ ?_
  rw [â† nhdsWithin_univ, â† Iic_union_Ioi, nhdsWithin_union, PredOrder.nhdsLE, SuccOrder.nhdsGT,
    sup_bot_eq]

end LinearOrder

section LinearOrder

variable [TopologicalSpace Î±] [LinearOrder Î±] [OrderClosedTopology Î±] {f g : Î² â†’ Î±}

section

variable [TopologicalSpace Î²]

theorem lt_subset_interior_le (hf : Continuous f) (hg : Continuous g) :
    { b | f b < g b } âŠ† interior { b | f b â‰¤ g b } :=
  (interior_maximal fun _ â†¦ le_of_lt) <| isOpen_lt hf hg

theorem frontier_le_subset_eq (hf : Continuous f) (hg : Continuous g) :
    frontier { b | f b â‰¤ g b } âŠ† { b | f b = g b } := by
  rw [frontier_eq_closure_inter_closure, closure_le_eq hf hg]
  rintro b âŸ¨hbâ‚, hbâ‚‚âŸ©
  refine le_antisymm hbâ‚ (closure_lt_subset_le hg hf ?_)
  convert hbâ‚‚ using 2; simp only [not_le.symm]; rfl

theorem frontier_Iic_subset (a : Î±) : frontier (Iic a) âŠ† {a} :=
  frontier_le_subset_eq (@continuous_id Î± _) continuous_const

theorem frontier_Ici_subset (a : Î±) : frontier (Ici a) âŠ† {a} :=
  frontier_Iic_subset (Î± := Î±áµ’áµˆ) _

theorem frontier_lt_subset_eq (hf : Continuous f) (hg : Continuous g) :
    frontier { b | f b < g b } âŠ† { b | f b = g b } := by
  simpa only [â† not_lt, â† compl_setOf, frontier_compl, eq_comm] using frontier_le_subset_eq hg hf

theorem continuous_if_le [TopologicalSpace Î³] [âˆ€ x, Decidable (f x â‰¤ g x)] {f' g' : Î² â†’ Î³}
    (hf : Continuous f) (hg : Continuous g) (hf' : ContinuousOn f' { x | f x â‰¤ g x })
    (hg' : ContinuousOn g' { x | g x â‰¤ f x }) (hfg : âˆ€ x, f x = g x â†’ f' x = g' x) :
    Continuous fun x â†¦ if f x â‰¤ g x then f' x else g' x := by
  refine continuous_if (fun a ha â†¦ hfg _ (frontier_le_subset_eq hf hg ha)) ?_ (hg'.mono ?_)
  Â· rwa [(isClosed_le hf hg).closure_eq]
  Â· simp only [not_le]
    exact closure_lt_subset_le hg hf

theorem Continuous.if_le [TopologicalSpace Î³] [âˆ€ x, Decidable (f x â‰¤ g x)] {f' g' : Î² â†’ Î³}
    (hf' : Continuous f') (hg' : Continuous g') (hf : Continuous f) (hg : Continuous g)
    (hfg : âˆ€ x, f x = g x â†’ f' x = g' x) : Continuous fun x â†¦ if f x â‰¤ g x then f' x else g' x :=
  continuous_if_le hf hg hf'.continuousOn hg'.continuousOn hfg

theorem Filter.Tendsto.eventually_lt {l : Filter Î³} {f g : Î³ â†’ Î±} {y z : Î±} (hf : Tendsto f l (ğ“ y))
    (hg : Tendsto g l (ğ“ z)) (hyz : y < z) : âˆ€á¶  x in l, f x < g x :=
  let âŸ¨_a, ha, _b, hb, hâŸ© := hyz.exists_disjoint_Iio_Ioi
  (hg.eventually (Ioi_mem_nhds hb)).mp <| (hf.eventually (Iio_mem_nhds ha)).mono fun _ hâ‚ hâ‚‚ â†¦
    h _ hâ‚ _ hâ‚‚

nonrec theorem ContinuousAt.eventually_lt {xâ‚€ : Î²} (hf : ContinuousAt f xâ‚€) (hg : ContinuousAt g xâ‚€)
    (hfg : f xâ‚€ < g xâ‚€) : âˆ€á¶  x in ğ“ xâ‚€, f x < g x :=
  hf.eventually_lt hg hfg

@[continuity, fun_prop]
protected theorem Continuous.min (hf : Continuous f) (hg : Continuous g) :
    Continuous fun b â†¦ min (f b) (g b) := by
  simp only [min_def]
  exact hf.if_le hg hf hg fun x â†¦ id

@[continuity, fun_prop]
protected theorem Continuous.max (hf : Continuous f) (hg : Continuous g) :
    Continuous fun b â†¦ max (f b) (g b) :=
  Continuous.min (Î± := Î±áµ’áµˆ) hf hg

end

theorem continuous_min : Continuous fun p : Î± Ã— Î± â†¦ min p.1 p.2 :=
  continuous_fst.min continuous_snd

theorem continuous_max : Continuous fun p : Î± Ã— Î± â†¦ max p.1 p.2 :=
  continuous_fst.max continuous_snd

protected theorem Filter.Tendsto.max {b : Filter Î²} {aâ‚ aâ‚‚ : Î±} (hf : Tendsto f b (ğ“ aâ‚))
    (hg : Tendsto g b (ğ“ aâ‚‚)) : Tendsto (fun b â†¦ max (f b) (g b)) b (ğ“ (max aâ‚ aâ‚‚)) :=
  (continuous_max.tendsto (aâ‚, aâ‚‚)).comp (hf.prodMk_nhds hg)

protected theorem Filter.Tendsto.min {b : Filter Î²} {aâ‚ aâ‚‚ : Î±} (hf : Tendsto f b (ğ“ aâ‚))
    (hg : Tendsto g b (ğ“ aâ‚‚)) : Tendsto (fun b â†¦ min (f b) (g b)) b (ğ“ (min aâ‚ aâ‚‚)) :=
  (continuous_min.tendsto (aâ‚, aâ‚‚)).comp (hf.prodMk_nhds hg)

protected theorem Filter.Tendsto.max_right {l : Filter Î²} {a : Î±} (h : Tendsto f l (ğ“ a)) :
    Tendsto (fun i â†¦ max a (f i)) l (ğ“ a) := by
  simpa only [sup_idem] using (tendsto_const_nhds (x := a)).max h

protected theorem Filter.Tendsto.max_left {l : Filter Î²} {a : Î±} (h : Tendsto f l (ğ“ a)) :
    Tendsto (fun i â†¦ max (f i) a) l (ğ“ a) := by
  simp_rw [max_comm _ a]
  exact h.max_right

theorem Filter.tendsto_nhds_max_right {l : Filter Î²} {a : Î±} (h : Tendsto f l (ğ“[>] a)) :
    Tendsto (fun i â†¦ max a (f i)) l (ğ“[>] a) := by
  obtain âŸ¨hâ‚ : Tendsto f l (ğ“ a), hâ‚‚ : âˆ€á¶  i in l, f i âˆˆ Ioi aâŸ© := tendsto_nhdsWithin_iff.mp h
  exact tendsto_nhdsWithin_iff.mpr âŸ¨hâ‚.max_right, hâ‚‚.mono fun i hi â†¦ lt_max_of_lt_right hiâŸ©

theorem Filter.tendsto_nhds_max_left {l : Filter Î²} {a : Î±} (h : Tendsto f l (ğ“[>] a)) :
    Tendsto (fun i â†¦ max (f i) a) l (ğ“[>] a) := by
  simp_rw [max_comm _ a]
  exact Filter.tendsto_nhds_max_right h

theorem Filter.Tendsto.min_right {l : Filter Î²} {a : Î±} (h : Tendsto f l (ğ“ a)) :
    Tendsto (fun i â†¦ min a (f i)) l (ğ“ a) :=
  Filter.Tendsto.max_right (Î± := Î±áµ’áµˆ) h

theorem Filter.Tendsto.min_left {l : Filter Î²} {a : Î±} (h : Tendsto f l (ğ“ a)) :
    Tendsto (fun i â†¦ min (f i) a) l (ğ“ a) :=
  Filter.Tendsto.max_left (Î± := Î±áµ’áµˆ) h

theorem Filter.tendsto_nhds_min_right {l : Filter Î²} {a : Î±} (h : Tendsto f l (ğ“[<] a)) :
    Tendsto (fun i â†¦ min a (f i)) l (ğ“[<] a) :=
  Filter.tendsto_nhds_max_right (Î± := Î±áµ’áµˆ) h

theorem Filter.tendsto_nhds_min_left {l : Filter Î²} {a : Î±} (h : Tendsto f l (ğ“[<] a)) :
    Tendsto (fun i â†¦ min (f i) a) l (ğ“[<] a) :=
  Filter.tendsto_nhds_max_left (Î± := Î±áµ’áµˆ) h

theorem Dense.exists_between [DenselyOrdered Î±] {s : Set Î±} (hs : Dense s) {x y : Î±} (h : x < y) :
    âˆƒ z âˆˆ s, z âˆˆ Ioo x y :=
  hs.exists_mem_open isOpen_Ioo (nonempty_Ioo.2 h)

theorem Dense.Ioi_eq_biUnion [DenselyOrdered Î±] {s : Set Î±} (hs : Dense s) (x : Î±) :
    Ioi x = â‹ƒ y âˆˆ s âˆ© Ioi x, Ioi y := by
  refine Subset.antisymm (fun z hz â†¦ ?_) (iUnionâ‚‚_subset fun y hy â†¦ Ioi_subset_Ioi (le_of_lt hy.2))
  rcases hs.exists_between hz with âŸ¨y, hys, hxy, hyzâŸ©
  exact mem_iUnionâ‚‚.2 âŸ¨y, âŸ¨hys, hxyâŸ©, hyzâŸ©

theorem Dense.Iio_eq_biUnion [DenselyOrdered Î±] {s : Set Î±} (hs : Dense s) (x : Î±) :
    Iio x = â‹ƒ y âˆˆ s âˆ© Iio x, Iio y :=
  Dense.Ioi_eq_biUnion (Î± := Î±áµ’áµˆ) hs x

end LinearOrder

end OrderClosedTopology

instance [Preorder Î±] [TopologicalSpace Î±] [OrderClosedTopology Î±] [Preorder Î²] [TopologicalSpace Î²]
    [OrderClosedTopology Î²] : OrderClosedTopology (Î± Ã— Î²) :=
  âŸ¨(isClosed_le continuous_fst.fst continuous_snd.fst).inter
    (isClosed_le continuous_fst.snd continuous_snd.snd)âŸ©

instance {Î¹ : Type*} {Î± : Î¹ â†’ Type*} [âˆ€ i, Preorder (Î± i)] [âˆ€ i, TopologicalSpace (Î± i)]
    [âˆ€ i, OrderClosedTopology (Î± i)] : OrderClosedTopology (âˆ€ i, Î± i) := by
  constructor
  simp only [Pi.le_def, setOf_forall]
  exact isClosed_iInter fun i â†¦ isClosed_le (continuous_apply i).fst' (continuous_apply i).snd'

instance Pi.orderClosedTopology' [Preorder Î²] [TopologicalSpace Î²] [OrderClosedTopology Î²] :
    OrderClosedTopology (Î± â†’ Î²) :=
  inferInstance
