/-
Copyright (c) 2021 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov, Alistair Tucker, Wen Yang
-/
import Mathlib.Order.Interval.Set.Image
import Mathlib.Order.CompleteLatticeIntervals
import Mathlib.Topology.Order.DenselyOrdered
import Mathlib.Topology.Order.Monotone

#align_import topology.algebra.order.intermediate_value from "leanprover-community/mathlib"@"4c19a16e4b705bf135cf9a80ac18fcc99c438514"

/-!
# Intermediate Value Theorem

In this file we prove the Intermediate Value Theorem: if `f : Î± â†’ Î²` is a function defined on a
connected set `s` that takes both values `â‰¤ a` and values `â‰¥ a` on `s`, then it is equal to `a` at
some point of `s`. We also prove that intervals in a dense conditionally complete order are
preconnected and any preconnected set is an interval. Then we specialize IVT to functions continuous
on intervals.

## Main results

* `IsPreconnected_I??` : all intervals `I??` are preconnected,
* `IsPreconnected.intermediate_value`, `intermediate_value_univ` : Intermediate Value Theorem for
  connected sets and connected spaces, respectively;
* `intermediate_value_Icc`, `intermediate_value_Icc'`: Intermediate Value Theorem for functions
  on closed intervals.

### Miscellaneous facts

* `IsClosed.Icc_subset_of_forall_mem_nhdsWithin` : â€œContinuous inductionâ€ principle;
  if `s âˆ© [a, b]` is closed, `a âˆˆ s`, and for each `x âˆˆ [a, b) âˆ© s` some of its right neighborhoods
  is included `s`, then `[a, b] âŠ† s`.
* `IsClosed.Icc_subset_of_forall_exists_gt`, `IsClosed.mem_of_ge_of_forall_exists_gt` : two
  other versions of the â€œcontinuous inductionâ€ principle.
* `ContinuousOn.StrictMonoOn_of_InjOn_Ioo` :
  Every continuous injective `f : (a, b) â†’ Î´` is strictly monotone
  or antitone (increasing or decreasing).

## Tags

intermediate value theorem, connected space, connected set
-/


open Filter OrderDual TopologicalSpace Function Set

open Topology Filter

universe u v w

/-!
### Intermediate value theorem on a (pre)connected space

In this section we prove the following theorem (see `IsPreconnected.intermediate_valueâ‚‚`): if `f`
and `g` are two functions continuous on a preconnected set `s`, `f a â‰¤ g a` at some `a âˆˆ s` and
`g b â‰¤ f b` at some `b âˆˆ s`, then `f c = g c` at some `c âˆˆ s`. We prove several versions of this
statement, including the classical IVT that corresponds to a constant function `g`.
-/


section

variable {X : Type u} {Î± : Type v} [TopologicalSpace X] [LinearOrder Î±] [TopologicalSpace Î±]
  [OrderClosedTopology Î±]

/-- Intermediate value theorem for two functions: if `f` and `g` are two continuous functions
on a preconnected space and `f a â‰¤ g a` and `g b â‰¤ f b`, then for some `x` we have `f x = g x`. -/
theorem intermediate_value_univâ‚‚ [PreconnectedSpace X] {a b : X} {f g : X â†’ Î±} (hf : Continuous f)
    (hg : Continuous g) (ha : f a â‰¤ g a) (hb : g b â‰¤ f b) : âˆƒ x, f x = g x := by
  obtain âŸ¨x, _, hfg, hgfâŸ© : (univ âˆ© { x | f x â‰¤ g x âˆ§ g x â‰¤ f x }).Nonempty :=
    isPreconnected_closed_iff.1 PreconnectedSpace.isPreconnected_univ _ _ (isClosed_le hf hg)
      (isClosed_le hg hf) (fun _ _ => le_total _ _) âŸ¨a, trivial, haâŸ© âŸ¨b, trivial, hbâŸ©
  exact âŸ¨x, le_antisymm hfg hgfâŸ©
#align intermediate_value_univâ‚‚ intermediate_value_univâ‚‚

theorem intermediate_value_univâ‚‚_eventuallyâ‚ [PreconnectedSpace X] {a : X} {l : Filter X} [NeBot l]
    {f g : X â†’ Î±} (hf : Continuous f) (hg : Continuous g) (ha : f a â‰¤ g a) (he : g â‰¤á¶ [l] f) :
    âˆƒ x, f x = g x :=
  let âŸ¨_, hâŸ© := he.exists; intermediate_value_univâ‚‚ hf hg ha h
#align intermediate_value_univâ‚‚_eventuallyâ‚ intermediate_value_univâ‚‚_eventuallyâ‚

theorem intermediate_value_univâ‚‚_eventuallyâ‚‚ [PreconnectedSpace X] {lâ‚ lâ‚‚ : Filter X} [NeBot lâ‚]
    [NeBot lâ‚‚] {f g : X â†’ Î±} (hf : Continuous f) (hg : Continuous g) (heâ‚ : f â‰¤á¶ [lâ‚] g)
    (heâ‚‚ : g â‰¤á¶ [lâ‚‚] f) : âˆƒ x, f x = g x :=
  let âŸ¨_, hâ‚âŸ© := heâ‚.exists
  let âŸ¨_, hâ‚‚âŸ© := heâ‚‚.exists
  intermediate_value_univâ‚‚ hf hg hâ‚ hâ‚‚
#align intermediate_value_univâ‚‚_eventuallyâ‚‚ intermediate_value_univâ‚‚_eventuallyâ‚‚

/-- Intermediate value theorem for two functions: if `f` and `g` are two functions continuous
on a preconnected set `s` and for some `a b âˆˆ s` we have `f a â‰¤ g a` and `g b â‰¤ f b`,
then for some `x âˆˆ s` we have `f x = g x`. -/
theorem IsPreconnected.intermediate_valueâ‚‚ {s : Set X} (hs : IsPreconnected s) {a b : X}
    (ha : a âˆˆ s) (hb : b âˆˆ s) {f g : X â†’ Î±} (hf : ContinuousOn f s) (hg : ContinuousOn g s)
    (ha' : f a â‰¤ g a) (hb' : g b â‰¤ f b) : âˆƒ x âˆˆ s, f x = g x :=
  let âŸ¨x, hxâŸ© :=
    @intermediate_value_univâ‚‚ s Î± _ _ _ _ (Subtype.preconnectedSpace hs) âŸ¨a, haâŸ© âŸ¨b, hbâŸ© _ _
      (continuousOn_iff_continuous_restrict.1 hf) (continuousOn_iff_continuous_restrict.1 hg) ha'
      hb'
  âŸ¨x, x.2, hxâŸ©
#align is_preconnected.intermediate_valueâ‚‚ IsPreconnected.intermediate_valueâ‚‚

theorem IsPreconnected.intermediate_valueâ‚‚_eventuallyâ‚ {s : Set X} (hs : IsPreconnected s) {a : X}
    {l : Filter X} (ha : a âˆˆ s) [NeBot l] (hl : l â‰¤ ð“Ÿ s) {f g : X â†’ Î±} (hf : ContinuousOn f s)
    (hg : ContinuousOn g s) (ha' : f a â‰¤ g a) (he : g â‰¤á¶ [l] f) : âˆƒ x âˆˆ s, f x = g x := by
  rw [continuousOn_iff_continuous_restrict] at hf hg
  obtain âŸ¨b, hâŸ© :=
    @intermediate_value_univâ‚‚_eventuallyâ‚ _ _ _ _ _ _ (Subtype.preconnectedSpace hs) âŸ¨a, haâŸ© _
      (comap_coe_neBot_of_le_principal hl) _ _ hf hg ha' (he.comap _)
  exact âŸ¨b, b.prop, hâŸ©
#align is_preconnected.intermediate_valueâ‚‚_eventuallyâ‚ IsPreconnected.intermediate_valueâ‚‚_eventuallyâ‚

theorem IsPreconnected.intermediate_valueâ‚‚_eventuallyâ‚‚ {s : Set X} (hs : IsPreconnected s)
    {lâ‚ lâ‚‚ : Filter X} [NeBot lâ‚] [NeBot lâ‚‚] (hlâ‚ : lâ‚ â‰¤ ð“Ÿ s) (hlâ‚‚ : lâ‚‚ â‰¤ ð“Ÿ s) {f g : X â†’ Î±}
    (hf : ContinuousOn f s) (hg : ContinuousOn g s) (heâ‚ : f â‰¤á¶ [lâ‚] g) (heâ‚‚ : g â‰¤á¶ [lâ‚‚] f) :
    âˆƒ x âˆˆ s, f x = g x := by
  rw [continuousOn_iff_continuous_restrict] at hf hg
  obtain âŸ¨b, hâŸ© :=
    @intermediate_value_univâ‚‚_eventuallyâ‚‚ _ _ _ _ _ _ (Subtype.preconnectedSpace hs) _ _
      (comap_coe_neBot_of_le_principal hlâ‚) (comap_coe_neBot_of_le_principal hlâ‚‚) _ _ hf hg
      (heâ‚.comap _) (heâ‚‚.comap _)
  exact âŸ¨b, b.prop, hâŸ©
#align is_preconnected.intermediate_valueâ‚‚_eventuallyâ‚‚ IsPreconnected.intermediate_valueâ‚‚_eventuallyâ‚‚

/-- **Intermediate Value Theorem** for continuous functions on connected sets. -/
theorem IsPreconnected.intermediate_value {s : Set X} (hs : IsPreconnected s) {a b : X} (ha : a âˆˆ s)
    (hb : b âˆˆ s) {f : X â†’ Î±} (hf : ContinuousOn f s) : Icc (f a) (f b) âŠ† f '' s := fun _x hx =>
  hs.intermediate_valueâ‚‚ ha hb hf continuousOn_const hx.1 hx.2
#align is_preconnected.intermediate_value IsPreconnected.intermediate_value

theorem IsPreconnected.intermediate_value_Ico {s : Set X} (hs : IsPreconnected s) {a : X}
    {l : Filter X} (ha : a âˆˆ s) [NeBot l] (hl : l â‰¤ ð“Ÿ s) {f : X â†’ Î±} (hf : ContinuousOn f s) {v : Î±}
    (ht : Tendsto f l (ð“ v)) : Ico (f a) v âŠ† f '' s := fun _ h =>
  hs.intermediate_valueâ‚‚_eventuallyâ‚ ha hl hf continuousOn_const h.1
    (eventually_ge_of_tendsto_gt h.2 ht)
#align is_preconnected.intermediate_value_Ico IsPreconnected.intermediate_value_Ico

theorem IsPreconnected.intermediate_value_Ioc {s : Set X} (hs : IsPreconnected s) {a : X}
    {l : Filter X} (ha : a âˆˆ s) [NeBot l] (hl : l â‰¤ ð“Ÿ s) {f : X â†’ Î±} (hf : ContinuousOn f s) {v : Î±}
    (ht : Tendsto f l (ð“ v)) : Ioc v (f a) âŠ† f '' s := fun _ h =>
  (hs.intermediate_valueâ‚‚_eventuallyâ‚ ha hl continuousOn_const hf h.2
    (eventually_le_of_tendsto_lt h.1 ht)).imp fun _ h => h.imp_right Eq.symm
#align is_preconnected.intermediate_value_Ioc IsPreconnected.intermediate_value_Ioc

theorem IsPreconnected.intermediate_value_Ioo {s : Set X} (hs : IsPreconnected s) {lâ‚ lâ‚‚ : Filter X}
    [NeBot lâ‚] [NeBot lâ‚‚] (hlâ‚ : lâ‚ â‰¤ ð“Ÿ s) (hlâ‚‚ : lâ‚‚ â‰¤ ð“Ÿ s) {f : X â†’ Î±} (hf : ContinuousOn f s)
    {vâ‚ vâ‚‚ : Î±} (htâ‚ : Tendsto f lâ‚ (ð“ vâ‚)) (htâ‚‚ : Tendsto f lâ‚‚ (ð“ vâ‚‚)) :
    Ioo vâ‚ vâ‚‚ âŠ† f '' s := fun _ h =>
  hs.intermediate_valueâ‚‚_eventuallyâ‚‚ hlâ‚ hlâ‚‚ hf continuousOn_const
    (eventually_le_of_tendsto_lt h.1 htâ‚) (eventually_ge_of_tendsto_gt h.2 htâ‚‚)
#align is_preconnected.intermediate_value_Ioo IsPreconnected.intermediate_value_Ioo

theorem IsPreconnected.intermediate_value_Ici {s : Set X} (hs : IsPreconnected s) {a : X}
    {l : Filter X} (ha : a âˆˆ s) [NeBot l] (hl : l â‰¤ ð“Ÿ s) {f : X â†’ Î±} (hf : ContinuousOn f s)
    (ht : Tendsto f l atTop) : Ici (f a) âŠ† f '' s := fun y h =>
  hs.intermediate_valueâ‚‚_eventuallyâ‚ ha hl hf continuousOn_const h (tendsto_atTop.1 ht y)
#align is_preconnected.intermediate_value_Ici IsPreconnected.intermediate_value_Ici

theorem IsPreconnected.intermediate_value_Iic {s : Set X} (hs : IsPreconnected s) {a : X}
    {l : Filter X} (ha : a âˆˆ s) [NeBot l] (hl : l â‰¤ ð“Ÿ s) {f : X â†’ Î±} (hf : ContinuousOn f s)
    (ht : Tendsto f l atBot) : Iic (f a) âŠ† f '' s := fun y h =>
  (hs.intermediate_valueâ‚‚_eventuallyâ‚ ha hl continuousOn_const hf h (tendsto_atBot.1 ht y)).imp
    fun _ h => h.imp_right Eq.symm
#align is_preconnected.intermediate_value_Iic IsPreconnected.intermediate_value_Iic

theorem IsPreconnected.intermediate_value_Ioi {s : Set X} (hs : IsPreconnected s) {lâ‚ lâ‚‚ : Filter X}
    [NeBot lâ‚] [NeBot lâ‚‚] (hlâ‚ : lâ‚ â‰¤ ð“Ÿ s) (hlâ‚‚ : lâ‚‚ â‰¤ ð“Ÿ s) {f : X â†’ Î±} (hf : ContinuousOn f s)
    {v : Î±} (htâ‚ : Tendsto f lâ‚ (ð“ v)) (htâ‚‚ : Tendsto f lâ‚‚ atTop) : Ioi v âŠ† f '' s := fun y h =>
  hs.intermediate_valueâ‚‚_eventuallyâ‚‚ hlâ‚ hlâ‚‚ hf continuousOn_const
    (eventually_le_of_tendsto_lt h htâ‚) (tendsto_atTop.1 htâ‚‚ y)
#align is_preconnected.intermediate_value_Ioi IsPreconnected.intermediate_value_Ioi

theorem IsPreconnected.intermediate_value_Iio {s : Set X} (hs : IsPreconnected s) {lâ‚ lâ‚‚ : Filter X}
    [NeBot lâ‚] [NeBot lâ‚‚] (hlâ‚ : lâ‚ â‰¤ ð“Ÿ s) (hlâ‚‚ : lâ‚‚ â‰¤ ð“Ÿ s) {f : X â†’ Î±} (hf : ContinuousOn f s)
    {v : Î±} (htâ‚ : Tendsto f lâ‚ atBot) (htâ‚‚ : Tendsto f lâ‚‚ (ð“ v)) : Iio v âŠ† f '' s := fun y h =>
  hs.intermediate_valueâ‚‚_eventuallyâ‚‚ hlâ‚ hlâ‚‚ hf continuousOn_const (tendsto_atBot.1 htâ‚ y)
    (eventually_ge_of_tendsto_gt h htâ‚‚)
#align is_preconnected.intermediate_value_Iio IsPreconnected.intermediate_value_Iio

theorem IsPreconnected.intermediate_value_Iii {s : Set X} (hs : IsPreconnected s) {lâ‚ lâ‚‚ : Filter X}
    [NeBot lâ‚] [NeBot lâ‚‚] (hlâ‚ : lâ‚ â‰¤ ð“Ÿ s) (hlâ‚‚ : lâ‚‚ â‰¤ ð“Ÿ s) {f : X â†’ Î±} (hf : ContinuousOn f s)
    (htâ‚ : Tendsto f lâ‚ atBot) (htâ‚‚ : Tendsto f lâ‚‚ atTop) : univ âŠ† f '' s := fun y _ =>
  hs.intermediate_valueâ‚‚_eventuallyâ‚‚ hlâ‚ hlâ‚‚ hf continuousOn_const (tendsto_atBot.1 htâ‚ y)
    (tendsto_atTop.1 htâ‚‚ y)
set_option linter.uppercaseLean3 false in
#align is_preconnected.intermediate_value_Iii IsPreconnected.intermediate_value_Iii

/-- **Intermediate Value Theorem** for continuous functions on connected spaces. -/
theorem intermediate_value_univ [PreconnectedSpace X] (a b : X) {f : X â†’ Î±} (hf : Continuous f) :
    Icc (f a) (f b) âŠ† range f := fun _ hx => intermediate_value_univâ‚‚ hf continuous_const hx.1 hx.2
#align intermediate_value_univ intermediate_value_univ

/-- **Intermediate Value Theorem** for continuous functions on connected spaces. -/
theorem mem_range_of_exists_le_of_exists_ge [PreconnectedSpace X] {c : Î±} {f : X â†’ Î±}
    (hf : Continuous f) (hâ‚ : âˆƒ a, f a â‰¤ c) (hâ‚‚ : âˆƒ b, c â‰¤ f b) : c âˆˆ range f :=
  let âŸ¨a, haâŸ© := hâ‚; let âŸ¨b, hbâŸ© := hâ‚‚; intermediate_value_univ a b hf âŸ¨ha, hbâŸ©
#align mem_range_of_exists_le_of_exists_ge mem_range_of_exists_le_of_exists_ge

/-!
### (Pre)connected sets in a linear order

In this section we prove the following results:

* `IsPreconnected.ordConnected`: any preconnected set `s` in a linear order is `OrdConnected`,
  i.e. `a âˆˆ s` and `b âˆˆ s` imply `Icc a b âŠ† s`;

* `IsPreconnected.mem_intervals`: any preconnected set `s` in a conditionally complete linear order
  is one of the intervals `Set.Icc`, `set.`Ico`, `set.Ioc`, `set.Ioo`, ``Set.Ici`, `Set.Iic`,
  `Set.Ioi`, `Set.Iio`; note that this is false for non-complete orders: e.g., in `â„ \ {0}`, the set
  of positive numbers cannot be represented as `Set.Ioi _`.

-/


/-- If a preconnected set contains endpoints of an interval, then it includes the whole interval. -/
theorem IsPreconnected.Icc_subset {s : Set Î±} (hs : IsPreconnected s) {a b : Î±} (ha : a âˆˆ s)
    (hb : b âˆˆ s) : Icc a b âŠ† s := by
  simpa only [image_id] using hs.intermediate_value ha hb continuousOn_id
#align is_preconnected.Icc_subset IsPreconnected.Icc_subset

theorem IsPreconnected.ordConnected {s : Set Î±} (h : IsPreconnected s) : OrdConnected s :=
  âŸ¨fun _ hx _ hy => h.Icc_subset hx hyâŸ©
#align is_preconnected.ord_connected IsPreconnected.ordConnected

/-- If a preconnected set contains endpoints of an interval, then it includes the whole interval. -/
theorem IsConnected.Icc_subset {s : Set Î±} (hs : IsConnected s) {a b : Î±} (ha : a âˆˆ s)
    (hb : b âˆˆ s) : Icc a b âŠ† s :=
  hs.2.Icc_subset ha hb
#align is_connected.Icc_subset IsConnected.Icc_subset

/-- If preconnected set in a linear order space is unbounded below and above, then it is the whole
space. -/
theorem IsPreconnected.eq_univ_of_unbounded {s : Set Î±} (hs : IsPreconnected s) (hb : Â¬BddBelow s)
    (ha : Â¬BddAbove s) : s = univ := by
  refine eq_univ_of_forall fun x => ?_
  obtain âŸ¨y, ys, hyâŸ© : âˆƒ y âˆˆ s, y < x := not_bddBelow_iff.1 hb x
  obtain âŸ¨z, zs, hzâŸ© : âˆƒ z âˆˆ s, x < z := not_bddAbove_iff.1 ha x
  exact hs.Icc_subset ys zs âŸ¨le_of_lt hy, le_of_lt hzâŸ©
#align is_preconnected.eq_univ_of_unbounded IsPreconnected.eq_univ_of_unbounded

end

variable {Î± : Type u} {Î² : Type v} {Î³ : Type w} [ConditionallyCompleteLinearOrder Î±]
  [TopologicalSpace Î±] [OrderTopology Î±] [ConditionallyCompleteLinearOrder Î²] [TopologicalSpace Î²]
  [OrderTopology Î²] [Nonempty Î³]

/-- A bounded connected subset of a conditionally complete linear order includes the open interval
`(Inf s, Sup s)`. -/
theorem IsConnected.Ioo_csInf_csSup_subset {s : Set Î±} (hs : IsConnected s) (hb : BddBelow s)
    (ha : BddAbove s) : Ioo (sInf s) (sSup s) âŠ† s := fun _x hx =>
  let âŸ¨_y, ys, hyâŸ© := (isGLB_lt_iff (isGLB_csInf hs.nonempty hb)).1 hx.1
  let âŸ¨_z, zs, hzâŸ© := (lt_isLUB_iff (isLUB_csSup hs.nonempty ha)).1 hx.2
  hs.Icc_subset ys zs âŸ¨hy.le, hz.leâŸ©
#align is_connected.Ioo_cInf_cSup_subset IsConnected.Ioo_csInf_csSup_subset

theorem eq_Icc_csInf_csSup_of_connected_bdd_closed {s : Set Î±} (hc : IsConnected s)
    (hb : BddBelow s) (ha : BddAbove s) (hcl : IsClosed s) : s = Icc (sInf s) (sSup s) :=
  (subset_Icc_csInf_csSup hb ha).antisymm <|
    hc.Icc_subset (hcl.csInf_mem hc.nonempty hb) (hcl.csSup_mem hc.nonempty ha)
#align eq_Icc_cInf_cSup_of_connected_bdd_closed eq_Icc_csInf_csSup_of_connected_bdd_closed

theorem IsPreconnected.Ioi_csInf_subset {s : Set Î±} (hs : IsPreconnected s) (hb : BddBelow s)
    (ha : Â¬BddAbove s) : Ioi (sInf s) âŠ† s := fun x hx =>
  have sne : s.Nonempty := nonempty_of_not_bddAbove ha
  let âŸ¨_y, ys, hyâŸ© : âˆƒ y âˆˆ s, y < x := (isGLB_lt_iff (isGLB_csInf sne hb)).1 hx
  let âŸ¨_z, zs, hzâŸ© : âˆƒ z âˆˆ s, x < z := not_bddAbove_iff.1 ha x
  hs.Icc_subset ys zs âŸ¨hy.le, hz.leâŸ©
#align is_preconnected.Ioi_cInf_subset IsPreconnected.Ioi_csInf_subset

theorem IsPreconnected.Iio_csSup_subset {s : Set Î±} (hs : IsPreconnected s) (hb : Â¬BddBelow s)
    (ha : BddAbove s) : Iio (sSup s) âŠ† s :=
  IsPreconnected.Ioi_csInf_subset (Î± := Î±áµ’áµˆ) hs ha hb
#align is_preconnected.Iio_cSup_subset IsPreconnected.Iio_csSup_subset

/-- A preconnected set in a conditionally complete linear order is either one of the intervals
`[Inf s, Sup s]`, `[Inf s, Sup s)`, `(Inf s, Sup s]`, `(Inf s, Sup s)`, `[Inf s, +âˆž)`,
`(Inf s, +âˆž)`, `(-âˆž, Sup s]`, `(-âˆž, Sup s)`, `(-âˆž, +âˆž)`, or `âˆ…`. The converse statement requires
`Î±` to be densely ordered. -/
theorem IsPreconnected.mem_intervals {s : Set Î±} (hs : IsPreconnected s) :
    s âˆˆ
      ({Icc (sInf s) (sSup s), Ico (sInf s) (sSup s), Ioc (sInf s) (sSup s), Ioo (sInf s) (sSup s),
          Ici (sInf s), Ioi (sInf s), Iic (sSup s), Iio (sSup s), univ, âˆ…} : Set (Set Î±)) := by
  rcases s.eq_empty_or_nonempty with (rfl | hne)
  Â· apply_rules [Or.inr, mem_singleton]
  have hs' : IsConnected s := âŸ¨hne, hsâŸ©
  by_cases hb : BddBelow s <;> by_cases ha : BddAbove s
  Â· refine mem_of_subset_of_mem ?_ <| mem_Icc_Ico_Ioc_Ioo_of_subset_of_subset
      (hs'.Ioo_csInf_csSup_subset hb ha) (subset_Icc_csInf_csSup hb ha)
    simp only [insert_subset_iff, mem_insert_iff, mem_singleton_iff, true_or, or_true,
      singleton_subset_iff, and_self]
  Â· refine Or.inr <| Or.inr <| Or.inr <| Or.inr ?_
    cases'
      mem_Ici_Ioi_of_subset_of_subset (hs.Ioi_csInf_subset hb ha) fun x hx => csInf_le hb hx with
      hs hs
    Â· exact Or.inl hs
    Â· exact Or.inr (Or.inl hs)
  Â· iterate 6 apply Or.inr
    cases' mem_Iic_Iio_of_subset_of_subset (hs.Iio_csSup_subset hb ha) fun x hx => le_csSup ha hx
      with hs hs
    Â· exact Or.inl hs
    Â· exact Or.inr (Or.inl hs)
  Â· iterate 8 apply Or.inr
    exact Or.inl (hs.eq_univ_of_unbounded hb ha)
#align is_preconnected.mem_intervals IsPreconnected.mem_intervals

/-- A preconnected set is either one of the intervals `Icc`, `Ico`, `Ioc`, `Ioo`, `Ici`, `Ioi`,
`Iic`, `Iio`, or `univ`, or `âˆ…`. The converse statement requires `Î±` to be densely ordered. Though
one can represent `âˆ…` as `(Inf âˆ…, Inf âˆ…)`, we include it into the list of possible cases to improve
readability. -/
theorem setOf_isPreconnected_subset_of_ordered :
    { s : Set Î± | IsPreconnected s } âŠ†
      -- bounded intervals
      (range (uncurry Icc) âˆª range (uncurry Ico) âˆª range (uncurry Ioc) âˆª range (uncurry Ioo)) âˆª
      -- unbounded intervals and `univ`
      (range Ici âˆª range Ioi âˆª range Iic âˆª range Iio âˆª {univ, âˆ…}) := by
  intro s hs
  rcases hs.mem_intervals with (hs | hs | hs | hs | hs | hs | hs | hs | hs | hs) <;> rw [hs] <;>
    simp only [union_insert, union_singleton, mem_insert_iff, mem_union, mem_range, Prod.exists,
      uncurry_apply_pair, exists_apply_eq_apply, true_or, or_true, exists_apply_eq_apply2]
#align set_of_is_preconnected_subset_of_ordered setOf_isPreconnected_subset_of_ordered

/-!
### Intervals are connected

In this section we prove that a closed interval (hence, any `OrdConnected` set) in a dense
conditionally complete linear order is preconnected.
-/


/-- A "continuous induction principle" for a closed interval: if a set `s` meets `[a, b]`
on a closed subset, contains `a`, and the set `s âˆ© [a, b)` has no maximal point, then `b âˆˆ s`. -/
theorem IsClosed.mem_of_ge_of_forall_exists_gt {a b : Î±} {s : Set Î±} (hs : IsClosed (s âˆ© Icc a b))
    (ha : a âˆˆ s) (hab : a â‰¤ b) (hgt : âˆ€ x âˆˆ s âˆ© Ico a b, (s âˆ© Ioc x b).Nonempty) : b âˆˆ s := by
  let S := s âˆ© Icc a b
  replace ha : a âˆˆ S := âŸ¨ha, left_mem_Icc.2 habâŸ©
  have Sbd : BddAbove S := âŸ¨b, fun z hz => hz.2.2âŸ©
  let c := sSup (s âˆ© Icc a b)
  have c_mem : c âˆˆ S := hs.csSup_mem âŸ¨_, haâŸ© Sbd
  have c_le : c â‰¤ b := csSup_le âŸ¨_, haâŸ© fun x hx => hx.2.2
  cases' eq_or_lt_of_le c_le with hc hc
  Â· exact hc â–¸ c_mem.1
  exfalso
  rcases hgt c âŸ¨c_mem.1, c_mem.2.1, hcâŸ© with âŸ¨x, xs, cx, xbâŸ©
  exact not_lt_of_le (le_csSup Sbd âŸ¨xs, le_trans (le_csSup Sbd ha) (le_of_lt cx), xbâŸ©) cx
#align is_closed.mem_of_ge_of_forall_exists_gt IsClosed.mem_of_ge_of_forall_exists_gt

/-- A "continuous induction principle" for a closed interval: if a set `s` meets `[a, b]`
on a closed subset, contains `a`, and for any `a â‰¤ x < y â‰¤ b`, `x âˆˆ s`, the set `s âˆ© (x, y]`
is not empty, then `[a, b] âŠ† s`. -/
theorem IsClosed.Icc_subset_of_forall_exists_gt {a b : Î±} {s : Set Î±} (hs : IsClosed (s âˆ© Icc a b))
    (ha : a âˆˆ s) (hgt : âˆ€ x âˆˆ s âˆ© Ico a b, âˆ€ y âˆˆ Ioi x, (s âˆ© Ioc x y).Nonempty) : Icc a b âŠ† s := by
  intro y hy
  have : IsClosed (s âˆ© Icc a y) := by
    suffices s âˆ© Icc a y = s âˆ© Icc a b âˆ© Icc a y by
      rw [this]
      exact IsClosed.inter hs isClosed_Icc
    rw [inter_assoc]
    congr
    exact (inter_eq_self_of_subset_right <| Icc_subset_Icc_right hy.2).symm
  exact
    IsClosed.mem_of_ge_of_forall_exists_gt this ha hy.1 fun x hx =>
      hgt x âŸ¨hx.1, Ico_subset_Ico_right hy.2 hx.2âŸ© y hx.2.2
#align is_closed.Icc_subset_of_forall_exists_gt IsClosed.Icc_subset_of_forall_exists_gt

variable [DenselyOrdered Î±] {a b : Î±}

/-- A "continuous induction principle" for a closed interval: if a set `s` meets `[a, b]`
on a closed subset, contains `a`, and for any `x âˆˆ s âˆ© [a, b)` the set `s` includes some open
neighborhood of `x` within `(x, +âˆž)`, then `[a, b] âŠ† s`. -/
theorem IsClosed.Icc_subset_of_forall_mem_nhdsWithin {a b : Î±} {s : Set Î±}
    (hs : IsClosed (s âˆ© Icc a b)) (ha : a âˆˆ s) (hgt : âˆ€ x âˆˆ s âˆ© Ico a b, s âˆˆ ð“[>] x) :
    Icc a b âŠ† s := by
  apply hs.Icc_subset_of_forall_exists_gt ha
  rintro x âŸ¨hxs, hxabâŸ© y hyxb
  have : s âˆ© Ioc x y âˆˆ ð“[>] x :=
    inter_mem (hgt x âŸ¨hxs, hxabâŸ©) (Ioc_mem_nhdsWithin_Ioi âŸ¨le_rfl, hyxbâŸ©)
  exact (nhdsWithin_Ioi_self_neBot' âŸ¨b, hxab.2âŸ©).nonempty_of_mem this
#align is_closed.Icc_subset_of_forall_mem_nhds_within IsClosed.Icc_subset_of_forall_mem_nhdsWithin

theorem isPreconnected_Icc_aux (x y : Î±) (s t : Set Î±) (hxy : x â‰¤ y) (hs : IsClosed s)
    (ht : IsClosed t) (hab : Icc a b âŠ† s âˆª t) (hx : x âˆˆ Icc a b âˆ© s) (hy : y âˆˆ Icc a b âˆ© t) :
    (Icc a b âˆ© (s âˆ© t)).Nonempty := by
  have xyab : Icc x y âŠ† Icc a b := Icc_subset_Icc hx.1.1 hy.1.2
  by_contra hst
  suffices Icc x y âŠ† s from
    hst âŸ¨y, xyab <| right_mem_Icc.2 hxy, this <| right_mem_Icc.2 hxy, hy.2âŸ©
  apply (IsClosed.inter hs isClosed_Icc).Icc_subset_of_forall_mem_nhdsWithin hx.2
  rintro z âŸ¨zs, hzâŸ©
  have zt : z âˆˆ tá¶œ := fun zt => hst âŸ¨z, xyab <| Ico_subset_Icc_self hz, zs, ztâŸ©
  have : tá¶œ âˆ© Ioc z y âˆˆ ð“[>] z := by
    rw [â† nhdsWithin_Ioc_eq_nhdsWithin_Ioi hz.2]
    exact mem_nhdsWithin.2 âŸ¨tá¶œ, ht.isOpen_compl, zt, Subset.rflâŸ©
  apply mem_of_superset this
  have : Ioc z y âŠ† s âˆª t := fun w hw => hab (xyab âŸ¨le_trans hz.1 (le_of_lt hw.1), hw.2âŸ©)
  exact fun w âŸ¨wt, wzyâŸ© => (this wzy).elim id fun h => (wt h).elim
#align is_preconnected_Icc_aux isPreconnected_Icc_aux

/-- A closed interval in a densely ordered conditionally complete linear order is preconnected. -/
theorem isPreconnected_Icc : IsPreconnected (Icc a b) :=
  isPreconnected_closed_iff.2
    (by
      rintro s t hs ht hab âŸ¨x, hxâŸ© âŸ¨y, hyâŸ©
      -- This used to use `wlog`, but it was causing timeouts.
      rcases le_total x y with h | h
      Â· exact isPreconnected_Icc_aux x y s t h hs ht hab hx hy
      Â· rw [inter_comm s t]
        rw [union_comm s t] at hab
        exact isPreconnected_Icc_aux y x t s h ht hs hab hy hx)
#align is_preconnected_Icc isPreconnected_Icc

theorem isPreconnected_uIcc : IsPreconnected (uIcc a b) :=
  isPreconnected_Icc
#align is_preconnected_uIcc isPreconnected_uIcc

theorem Set.OrdConnected.isPreconnected {s : Set Î±} (h : s.OrdConnected) : IsPreconnected s :=
  isPreconnected_of_forall_pair fun x hx y hy =>
    âŸ¨uIcc x y, h.uIcc_subset hx hy, left_mem_uIcc, right_mem_uIcc, isPreconnected_uIccâŸ©
#align set.ord_connected.is_preconnected Set.OrdConnected.isPreconnected

theorem isPreconnected_iff_ordConnected {s : Set Î±} : IsPreconnected s â†” OrdConnected s :=
  âŸ¨IsPreconnected.ordConnected, Set.OrdConnected.isPreconnectedâŸ©
#align is_preconnected_iff_ord_connected isPreconnected_iff_ordConnected

theorem isPreconnected_Ici : IsPreconnected (Ici a) :=
  ordConnected_Ici.isPreconnected
#align is_preconnected_Ici isPreconnected_Ici

theorem isPreconnected_Iic : IsPreconnected (Iic a) :=
  ordConnected_Iic.isPreconnected
#align is_preconnected_Iic isPreconnected_Iic

theorem isPreconnected_Iio : IsPreconnected (Iio a) :=
  ordConnected_Iio.isPreconnected
#align is_preconnected_Iio isPreconnected_Iio

theorem isPreconnected_Ioi : IsPreconnected (Ioi a) :=
  ordConnected_Ioi.isPreconnected
#align is_preconnected_Ioi isPreconnected_Ioi

theorem isPreconnected_Ioo : IsPreconnected (Ioo a b) :=
  ordConnected_Ioo.isPreconnected
#align is_preconnected_Ioo isPreconnected_Ioo

theorem isPreconnected_Ioc : IsPreconnected (Ioc a b) :=
  ordConnected_Ioc.isPreconnected
#align is_preconnected_Ioc isPreconnected_Ioc

theorem isPreconnected_Ico : IsPreconnected (Ico a b) :=
  ordConnected_Ico.isPreconnected
#align is_preconnected_Ico isPreconnected_Ico

theorem isConnected_Ici : IsConnected (Ici a) :=
  âŸ¨nonempty_Ici, isPreconnected_IciâŸ©
#align is_connected_Ici isConnected_Ici

theorem isConnected_Iic : IsConnected (Iic a) :=
  âŸ¨nonempty_Iic, isPreconnected_IicâŸ©
#align is_connected_Iic isConnected_Iic

theorem isConnected_Ioi [NoMaxOrder Î±] : IsConnected (Ioi a) :=
  âŸ¨nonempty_Ioi, isPreconnected_IoiâŸ©
#align is_connected_Ioi isConnected_Ioi

theorem isConnected_Iio [NoMinOrder Î±] : IsConnected (Iio a) :=
  âŸ¨nonempty_Iio, isPreconnected_IioâŸ©
#align is_connected_Iio isConnected_Iio

theorem isConnected_Icc (h : a â‰¤ b) : IsConnected (Icc a b) :=
  âŸ¨nonempty_Icc.2 h, isPreconnected_IccâŸ©
#align is_connected_Icc isConnected_Icc

theorem isConnected_Ioo (h : a < b) : IsConnected (Ioo a b) :=
  âŸ¨nonempty_Ioo.2 h, isPreconnected_IooâŸ©
#align is_connected_Ioo isConnected_Ioo

theorem isConnected_Ioc (h : a < b) : IsConnected (Ioc a b) :=
  âŸ¨nonempty_Ioc.2 h, isPreconnected_IocâŸ©
#align is_connected_Ioc isConnected_Ioc

theorem isConnected_Ico (h : a < b) : IsConnected (Ico a b) :=
  âŸ¨nonempty_Ico.2 h, isPreconnected_IcoâŸ©
#align is_connected_Ico isConnected_Ico

instance (priority := 100) ordered_connected_space : PreconnectedSpace Î± :=
  âŸ¨ordConnected_univ.isPreconnectedâŸ©
#align ordered_connected_space ordered_connected_space

/-- In a dense conditionally complete linear order, the set of preconnected sets is exactly
the set of the intervals `Icc`, `Ico`, `Ioc`, `Ioo`, `Ici`, `Ioi`, `Iic`, `Iio`, `(-âˆž, +âˆž)`,
or `âˆ…`. Though one can represent `âˆ…` as `(sInf s, sInf s)`, we include it into the list of
possible cases to improve readability. -/
theorem setOf_isPreconnected_eq_of_ordered :
    { s : Set Î± | IsPreconnected s } =
      -- bounded intervals
      range (uncurry Icc) âˆª range (uncurry Ico) âˆª range (uncurry Ioc) âˆª range (uncurry Ioo) âˆª
      -- unbounded intervals and `univ`
      (range Ici âˆª range Ioi âˆª range Iic âˆª range Iio âˆª {univ, âˆ…}) := by
  refine Subset.antisymm setOf_isPreconnected_subset_of_ordered ?_
  simp only [subset_def, forall_mem_range, uncurry, or_imp, forall_and, mem_union,
    mem_setOf_eq, insert_eq, mem_singleton_iff, forall_eq, forall_true_iff, and_true_iff,
    isPreconnected_Icc, isPreconnected_Ico, isPreconnected_Ioc, isPreconnected_Ioo,
    isPreconnected_Ioi, isPreconnected_Iio, isPreconnected_Ici, isPreconnected_Iic,
    isPreconnected_univ, isPreconnected_empty]
#align set_of_is_preconnected_eq_of_ordered setOf_isPreconnected_eq_of_ordered

/-- This lemmas characterizes when a subset `s` of a densely ordered conditionally complete linear
order is totally disconnected with respect to the order topology: between any two distinct points
of `s` must lie a point not in `s`. -/
lemma isTotallyDisconnected_iff_lt {s : Set Î±} :
    IsTotallyDisconnected s â†” âˆ€ x âˆˆ s, âˆ€ y âˆˆ s, x < y â†’ âˆƒ z âˆ‰ s, z âˆˆ Ioo x y := by
  simp only [IsTotallyDisconnected, isPreconnected_iff_ordConnected, â† not_nontrivial_iff,
    nontrivial_iff_exists_lt, not_exists, not_and]
  refine âŸ¨fun h x hx y hy hxy â†¦ ?_, fun h t hts ht x hx y hy hxy â†¦ ?_âŸ©
  Â· simp_rw [â† not_ordConnected_inter_Icc_iff hx hy]
    exact fun hs â†¦ h _ inter_subset_left hs _ âŸ¨hx, le_rfl, hxy.leâŸ© _ âŸ¨hy, hxy.le, le_rflâŸ© hxy
  Â· obtain âŸ¨z, h1z, h2zâŸ© := h x (hts hx) y (hts hy) hxy
    exact h1z <| hts <| ht.1 hx hy âŸ¨h2z.1.le, h2z.2.leâŸ©

/-!
### Intermediate Value Theorem on an interval

In this section we prove several versions of the Intermediate Value Theorem for a function
continuous on an interval.
-/


variable {Î´ : Type*} [LinearOrder Î´] [TopologicalSpace Î´] [OrderClosedTopology Î´]

/-- **Intermediate Value Theorem** for continuous functions on closed intervals, case
`f a â‰¤ t â‰¤ f b`. -/
theorem intermediate_value_Icc {a b : Î±} (hab : a â‰¤ b) {f : Î± â†’ Î´} (hf : ContinuousOn f (Icc a b)) :
    Icc (f a) (f b) âŠ† f '' Icc a b :=
  isPreconnected_Icc.intermediate_value (left_mem_Icc.2 hab) (right_mem_Icc.2 hab) hf
#align intermediate_value_Icc intermediate_value_Icc

/-- **Intermediate Value Theorem** for continuous functions on closed intervals, case
`f a â‰¥ t â‰¥ f b`. -/
theorem intermediate_value_Icc' {a b : Î±} (hab : a â‰¤ b) {f : Î± â†’ Î´}
    (hf : ContinuousOn f (Icc a b)) : Icc (f b) (f a) âŠ† f '' Icc a b :=
  isPreconnected_Icc.intermediate_value (right_mem_Icc.2 hab) (left_mem_Icc.2 hab) hf
#align intermediate_value_Icc' intermediate_value_Icc'

/-- **Intermediate Value Theorem** for continuous functions on closed intervals, unordered case. -/
theorem intermediate_value_uIcc {a b : Î±} {f : Î± â†’ Î´} (hf : ContinuousOn f (uIcc a b)) :
    uIcc (f a) (f b) âŠ† f '' uIcc a b := by
  cases le_total (f a) (f b) <;> simp [*, isPreconnected_uIcc.intermediate_value]
#align intermediate_value_uIcc intermediate_value_uIcc

theorem intermediate_value_Ico {a b : Î±} (hab : a â‰¤ b) {f : Î± â†’ Î´} (hf : ContinuousOn f (Icc a b)) :
    Ico (f a) (f b) âŠ† f '' Ico a b :=
  Or.elim (eq_or_lt_of_le hab) (fun he _ h => absurd h.2 (not_lt_of_le (he â–¸ h.1))) fun hlt =>
    @IsPreconnected.intermediate_value_Ico _ _ _ _ _ _ _ isPreconnected_Ico _ _ âŸ¨refl a, hltâŸ©
      (right_nhdsWithin_Ico_neBot hlt) inf_le_right _ (hf.mono Ico_subset_Icc_self) _
      ((hf.continuousWithinAt âŸ¨hab, refl bâŸ©).mono Ico_subset_Icc_self)
#align intermediate_value_Ico intermediate_value_Ico

theorem intermediate_value_Ico' {a b : Î±} (hab : a â‰¤ b) {f : Î± â†’ Î´}
    (hf : ContinuousOn f (Icc a b)) : Ioc (f b) (f a) âŠ† f '' Ico a b :=
  Or.elim (eq_or_lt_of_le hab) (fun he _ h => absurd h.1 (not_lt_of_le (he â–¸ h.2))) fun hlt =>
    @IsPreconnected.intermediate_value_Ioc _ _ _ _ _ _ _ isPreconnected_Ico _ _ âŸ¨refl a, hltâŸ©
      (right_nhdsWithin_Ico_neBot hlt) inf_le_right _ (hf.mono Ico_subset_Icc_self) _
      ((hf.continuousWithinAt âŸ¨hab, refl bâŸ©).mono Ico_subset_Icc_self)
#align intermediate_value_Ico' intermediate_value_Ico'

theorem intermediate_value_Ioc {a b : Î±} (hab : a â‰¤ b) {f : Î± â†’ Î´} (hf : ContinuousOn f (Icc a b)) :
    Ioc (f a) (f b) âŠ† f '' Ioc a b :=
  Or.elim (eq_or_lt_of_le hab) (fun he _ h => absurd h.2 (not_le_of_lt (he â–¸ h.1))) fun hlt =>
    @IsPreconnected.intermediate_value_Ioc _ _ _ _ _ _ _ isPreconnected_Ioc _ _ âŸ¨hlt, refl bâŸ©
      (left_nhdsWithin_Ioc_neBot hlt) inf_le_right _ (hf.mono Ioc_subset_Icc_self) _
      ((hf.continuousWithinAt âŸ¨refl a, habâŸ©).mono Ioc_subset_Icc_self)
#align intermediate_value_Ioc intermediate_value_Ioc

theorem intermediate_value_Ioc' {a b : Î±} (hab : a â‰¤ b) {f : Î± â†’ Î´}
    (hf : ContinuousOn f (Icc a b)) : Ico (f b) (f a) âŠ† f '' Ioc a b :=
  Or.elim (eq_or_lt_of_le hab) (fun he _ h => absurd h.1 (not_le_of_lt (he â–¸ h.2))) fun hlt =>
    @IsPreconnected.intermediate_value_Ico _ _ _ _ _ _ _ isPreconnected_Ioc _ _ âŸ¨hlt, refl bâŸ©
      (left_nhdsWithin_Ioc_neBot hlt) inf_le_right _ (hf.mono Ioc_subset_Icc_self) _
      ((hf.continuousWithinAt âŸ¨refl a, habâŸ©).mono Ioc_subset_Icc_self)
#align intermediate_value_Ioc' intermediate_value_Ioc'

theorem intermediate_value_Ioo {a b : Î±} (hab : a â‰¤ b) {f : Î± â†’ Î´} (hf : ContinuousOn f (Icc a b)) :
    Ioo (f a) (f b) âŠ† f '' Ioo a b :=
  Or.elim (eq_or_lt_of_le hab) (fun he _ h => absurd h.2 (not_lt_of_lt (he â–¸ h.1))) fun hlt =>
    @IsPreconnected.intermediate_value_Ioo _ _ _ _ _ _ _ isPreconnected_Ioo _ _
      (left_nhdsWithin_Ioo_neBot hlt) (right_nhdsWithin_Ioo_neBot hlt) inf_le_right inf_le_right _
      (hf.mono Ioo_subset_Icc_self) _ _
      ((hf.continuousWithinAt âŸ¨refl a, habâŸ©).mono Ioo_subset_Icc_self)
      ((hf.continuousWithinAt âŸ¨hab, refl bâŸ©).mono Ioo_subset_Icc_self)
#align intermediate_value_Ioo intermediate_value_Ioo

theorem intermediate_value_Ioo' {a b : Î±} (hab : a â‰¤ b) {f : Î± â†’ Î´}
    (hf : ContinuousOn f (Icc a b)) : Ioo (f b) (f a) âŠ† f '' Ioo a b :=
  Or.elim (eq_or_lt_of_le hab) (fun he _ h => absurd h.1 (not_lt_of_lt (he â–¸ h.2))) fun hlt =>
    @IsPreconnected.intermediate_value_Ioo _ _ _ _ _ _ _ isPreconnected_Ioo _ _
      (right_nhdsWithin_Ioo_neBot hlt) (left_nhdsWithin_Ioo_neBot hlt) inf_le_right inf_le_right _
      (hf.mono Ioo_subset_Icc_self) _ _
      ((hf.continuousWithinAt âŸ¨hab, refl bâŸ©).mono Ioo_subset_Icc_self)
      ((hf.continuousWithinAt âŸ¨refl a, habâŸ©).mono Ioo_subset_Icc_self)
#align intermediate_value_Ioo' intermediate_value_Ioo'

/-- **Intermediate value theorem**: if `f` is continuous on an order-connected set `s` and `a`,
`b` are two points of this set, then `f` sends `s` to a superset of `Icc (f x) (f y)`. -/
theorem ContinuousOn.surjOn_Icc {s : Set Î±} [hs : OrdConnected s] {f : Î± â†’ Î´}
    (hf : ContinuousOn f s) {a b : Î±} (ha : a âˆˆ s) (hb : b âˆˆ s) : SurjOn f s (Icc (f a) (f b)) :=
  hs.isPreconnected.intermediate_value ha hb hf
#align continuous_on.surj_on_Icc ContinuousOn.surjOn_Icc

/-- **Intermediate value theorem**: if `f` is continuous on an order-connected set `s` and `a`,
`b` are two points of this set, then `f` sends `s` to a superset of `[f x, f y]`. -/
theorem ContinuousOn.surjOn_uIcc {s : Set Î±} [hs : OrdConnected s] {f : Î± â†’ Î´}
    (hf : ContinuousOn f s) {a b : Î±} (ha : a âˆˆ s) (hb : b âˆˆ s) :
    SurjOn f s (uIcc (f a) (f b)) := by
  rcases le_total (f a) (f b) with hab | hab <;> simp [hf.surjOn_Icc, *]
#align continuous_on.surj_on_uIcc ContinuousOn.surjOn_uIcc

/-- A continuous function which tendsto `Filter.atTop` along `Filter.atTop` and to `atBot` along
`at_bot` is surjective. -/
theorem Continuous.surjective {f : Î± â†’ Î´} (hf : Continuous f) (h_top : Tendsto f atTop atTop)
    (h_bot : Tendsto f atBot atBot) : Function.Surjective f := fun p =>
  mem_range_of_exists_le_of_exists_ge hf (h_bot.eventually (eventually_le_atBot p)).exists
    (h_top.eventually (eventually_ge_atTop p)).exists
#align continuous.surjective Continuous.surjective

/-- A continuous function which tendsto `Filter.atBot` along `Filter.atTop` and to `Filter.atTop`
along `atBot` is surjective. -/
theorem Continuous.surjective' {f : Î± â†’ Î´} (hf : Continuous f) (h_top : Tendsto f atBot atTop)
    (h_bot : Tendsto f atTop atBot) : Function.Surjective f :=
  Continuous.surjective (Î± := Î±áµ’áµˆ) hf h_top h_bot
#align continuous.surjective' Continuous.surjective'

/-- If a function `f : Î± â†’ Î²` is continuous on a nonempty interval `s`, its restriction to `s`
tends to `at_bot : Filter Î²` along `at_bot : Filter â†¥s` and tends to `Filter.atTop : Filter Î²` along
`Filter.atTop : Filter â†¥s`, then the restriction of `f` to `s` is surjective. We formulate the
conclusion as `Function.surjOn f s Set.univ`. -/
theorem ContinuousOn.surjOn_of_tendsto {f : Î± â†’ Î´} {s : Set Î±} [OrdConnected s] (hs : s.Nonempty)
    (hf : ContinuousOn f s) (hbot : Tendsto (fun x : s => f x) atBot atBot)
    (htop : Tendsto (fun x : s => f x) atTop atTop) : SurjOn f s univ :=
  haveI := Classical.inhabited_of_nonempty hs.to_subtype
  surjOn_iff_surjective.2 <| hf.restrict.surjective htop hbot
#align continuous_on.surj_on_of_tendsto ContinuousOn.surjOn_of_tendsto

/-- If a function `f : Î± â†’ Î²` is continuous on a nonempty interval `s`, its restriction to `s`
tends to `Filter.atTop : Filter Î²` along `Filter.atBot : Filter â†¥s` and tends to
`Filter.atBot : Filter Î²` along `Filter.atTop : Filter â†¥s`, then the restriction of `f` to `s` is
surjective. We formulate the conclusion as `Function.surjOn f s Set.univ`. -/
theorem ContinuousOn.surjOn_of_tendsto' {f : Î± â†’ Î´} {s : Set Î±} [OrdConnected s] (hs : s.Nonempty)
    (hf : ContinuousOn f s) (hbot : Tendsto (fun x : s => f x) atBot atTop)
    (htop : Tendsto (fun x : s => f x) atTop atBot) : SurjOn f s univ :=
  ContinuousOn.surjOn_of_tendsto (Î´ := Î´áµ’áµˆ) hs hf hbot htop
#align continuous_on.surj_on_of_tendsto' ContinuousOn.surjOn_of_tendsto'

theorem Continuous.strictMono_of_inj_boundedOrder [BoundedOrder Î±] {f : Î± â†’ Î´}
    (hf_c : Continuous f) (hf : f âŠ¥ â‰¤ f âŠ¤) (hf_i : Injective f) : StrictMono f := by
  intro a b hab
  by_contra! h
  have H : f b < f a := lt_of_le_of_ne h <| hf_i.ne hab.ne'
  by_cases ha : f a â‰¤ f âŠ¥
  Â· obtain âŸ¨u, huâŸ© := intermediate_value_Ioc le_top hf_c.continuousOn âŸ¨H.trans_le ha, hfâŸ©
    have : u = âŠ¥ := hf_i hu.2
    aesop
  Â· by_cases hb : f âŠ¥ < f b
    Â· obtain âŸ¨u, huâŸ© := intermediate_value_Ioo bot_le hf_c.continuousOn âŸ¨hb, HâŸ©
      rw [hf_i hu.2] at hu
      exact (hab.trans hu.1.2).false
    Â· push_neg at ha hb
      replace hb : f b < f âŠ¥ := lt_of_le_of_ne hb <| hf_i.ne (lt_of_lt_of_le' hab bot_le).ne'
      obtain âŸ¨u, huâŸ© := intermediate_value_Ioo' hab.le hf_c.continuousOn âŸ¨hb, haâŸ©
      have : u = âŠ¥ := hf_i hu.2
      aesop

theorem Continuous.strictAnti_of_inj_boundedOrder [BoundedOrder Î±] {f : Î± â†’ Î´}
    (hf_c : Continuous f) (hf : f âŠ¤ â‰¤ f âŠ¥) (hf_i : Injective f) : StrictAnti f :=
  hf_c.strictMono_of_inj_boundedOrder (Î´ := Î´áµ’áµˆ) hf hf_i

theorem Continuous.strictMono_of_inj_boundedOrder' [BoundedOrder Î±] {f : Î± â†’ Î´}
    (hf_c : Continuous f) (hf_i : Injective f) : StrictMono f âˆ¨ StrictAnti f :=
  (le_total (f âŠ¥) (f âŠ¤)).imp
    (hf_c.strictMono_of_inj_boundedOrder Â· hf_i)
    (hf_c.strictAnti_of_inj_boundedOrder Â· hf_i)

set_option backward.synthInstance.canonInstances false in -- See https://github.com/leanprover-community/mathlib4/issues/12532
/-- Suppose `Î±` is equipped with a conditionally complete linear dense order and `f : Î± â†’ Î´` is
continuous and injective. Then `f` is strictly monotone (increasing) if
it is strictly monotone (increasing) on some closed interval `[a, b]`. -/
theorem Continuous.strictMonoOn_of_inj_rigidity {f : Î± â†’ Î´}
    (hf_c : Continuous f) (hf_i : Injective f) {a b : Î±} (hab : a < b)
    (hf_mono : StrictMonoOn f (Icc a b)) : StrictMono f := by
  intro x y hxy
  let s := min a x
  let t := max b y
  have hsa : s â‰¤ a := min_le_left a x
  have hbt : b â‰¤ t := le_max_left b y
  have hst : s â‰¤ t := hsa.trans $ hbt.trans' hab.le
  have hf_mono_st : StrictMonoOn f (Icc s t) âˆ¨ StrictAntiOn f (Icc s t) := by
    letI := Icc.completeLinearOrder hst
    have := Continuous.strictMono_of_inj_boundedOrder' (f := Set.restrict (Icc s t) f)
      hf_c.continuousOn.restrict hf_i.injOn.injective
    exact this.imp strictMono_restrict.mp strictAntiOn_iff_strictAnti.mpr
  have (h : StrictAntiOn f (Icc s t)) : False := by
    have : Icc a b âŠ† Icc s t := Icc_subset_Icc hsa hbt
    replace : StrictAntiOn f (Icc a b) := StrictAntiOn.mono h this
    replace : IsAntichain (Â· â‰¤ Â·) (Icc a b) :=
      IsAntichain.of_strictMonoOn_antitoneOn hf_mono this.antitoneOn
    exact this.not_lt (left_mem_Icc.mpr (le_of_lt hab)) (right_mem_Icc.mpr (le_of_lt hab)) hab
  replace hf_mono_st : StrictMonoOn f (Icc s t) := hf_mono_st.resolve_right this
  have hsx : s â‰¤ x := min_le_right a x
  have hyt : y â‰¤ t := le_max_right b y
  replace : Icc x y âŠ† Icc s t := Icc_subset_Icc hsx hyt
  replace : StrictMonoOn f (Icc x y) := StrictMonoOn.mono hf_mono_st this
  exact this (left_mem_Icc.mpr (le_of_lt hxy)) (right_mem_Icc.mpr (le_of_lt hxy)) hxy

set_option backward.synthInstance.canonInstances false in -- See https://github.com/leanprover-community/mathlib4/issues/12532
/-- Suppose `f : [a, b] â†’ Î´` is
continuous and injective. Then `f` is strictly monotone (increasing) if `f(a) â‰¤ f(b)`. -/
theorem ContinuousOn.strictMonoOn_of_injOn_Icc {a b : Î±} {f : Î± â†’ Î´}
    (hab : a â‰¤ b) (hfab : f a â‰¤ f b)
    (hf_c : ContinuousOn f (Icc a b)) (hf_i : InjOn f (Icc a b)) :
    StrictMonoOn f (Icc a b) := by
  letI := Icc.completeLinearOrder hab
  refine StrictMono.of_restrict ?_
  set g : Icc a b â†’ Î´ := Set.restrict (Icc a b) f
  have hgab : g âŠ¥ â‰¤ g âŠ¤ := by aesop
  exact Continuous.strictMono_of_inj_boundedOrder (f := g) hf_c.restrict hgab hf_i.injective

/-- Suppose `f : [a, b] â†’ Î´` is
continuous and injective. Then `f` is strictly antitone (decreasing) if `f(b) â‰¤ f(a)`. -/
theorem ContinuousOn.strictAntiOn_of_injOn_Icc {a b : Î±} {f : Î± â†’ Î´}
    (hab : a â‰¤ b) (hfab : f b â‰¤ f a)
    (hf_c : ContinuousOn f (Icc a b)) (hf_i : InjOn f (Icc a b)) :
    StrictAntiOn f (Icc a b) := ContinuousOn.strictMonoOn_of_injOn_Icc (Î´ := Î´áµ’áµˆ) hab hfab hf_c hf_i

/-- Suppose `f : [a, b] â†’ Î´` is continuous and injective. Then `f` is strictly monotone
or antitone (increasing or decreasing). -/
theorem ContinuousOn.strictMonoOn_of_injOn_Icc' {a b : Î±} {f : Î± â†’ Î´} (hab : a â‰¤ b)
    (hf_c : ContinuousOn f (Icc a b)) (hf_i : InjOn f (Icc a b)) :
    StrictMonoOn f (Icc a b) âˆ¨ StrictAntiOn f (Icc a b) :=
  (le_total (f a) (f b)).imp
    (ContinuousOn.strictMonoOn_of_injOn_Icc hab Â· hf_c hf_i)
    (ContinuousOn.strictAntiOn_of_injOn_Icc hab Â· hf_c hf_i)

/-- Suppose `Î±` is equipped with a conditionally complete linear dense order and `f : Î± â†’ Î´` is
continuous and injective. Then `f` is strictly monotone or antitone (increasing or decreasing). -/
theorem Continuous.strictMono_of_inj {f : Î± â†’ Î´}
    (hf_c : Continuous f) (hf_i : Injective f) : StrictMono f âˆ¨ StrictAnti f := by
  have H {c d : Î±} (hcd : c < d) : StrictMono f âˆ¨ StrictAnti f :=
    (hf_c.continuousOn.strictMonoOn_of_injOn_Icc' hcd.le hf_i.injOn).imp
      (hf_c.strictMonoOn_of_inj_rigidity hf_i hcd)
      (hf_c.strictMonoOn_of_inj_rigidity (Î´ := Î´áµ’áµˆ) hf_i hcd)
  by_cases hn : Nonempty Î±
  Â· let a : Î± := Classical.choice â€¹_â€º
    by_cases h : âˆƒ b : Î±, a â‰  b
    Â· choose b hb using h
      by_cases hab : a < b
      Â· exact H hab
      Â· push_neg at hab
        have : b < a := by exact Ne.lt_of_le (id (Ne.symm hb)) hab
        exact H this
    Â· push_neg at h
      haveI : Subsingleton Î± := âŸ¨fun c d => Trans.trans (h c).symm (h d)âŸ©
      exact Or.inl <| Subsingleton.strictMono f
  Â· aesop

/-- Every continuous injective `f : (a, b) â†’ Î´` is strictly monotone
or antitone (increasing or decreasing). -/
theorem ContinuousOn.strictMonoOn_of_injOn_Ioo {a b : Î±} {f : Î± â†’ Î´} (hab : a < b)
    (hf_c : ContinuousOn f (Ioo a b)) (hf_i : InjOn f (Ioo a b)) :
    StrictMonoOn f (Ioo a b) âˆ¨ StrictAntiOn f (Ioo a b) := by
  haveI : Inhabited (Ioo a b) := Classical.inhabited_of_nonempty (nonempty_Ioo_subtype hab)
  let g : Ioo a b â†’ Î´ := Set.restrict (Ioo a b) f
  have : StrictMono g âˆ¨ StrictAnti g :=
    Continuous.strictMono_of_inj hf_c.restrict hf_i.injective
  exact this.imp strictMono_restrict.mp strictAntiOn_iff_strictAnti.mpr
