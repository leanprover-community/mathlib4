/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Alistair Tucker, Wen Yang
-/
import Mathlib.Order.Interval.Set.Image
import Mathlib.Order.CompleteLatticeIntervals
import Mathlib.Topology.Order.DenselyOrdered
import Mathlib.Topology.Order.Monotone
import Mathlib.Topology.Connected.TotallyDisconnected

/-!
# Intermediate Value Theorem

In this file we prove the Intermediate Value Theorem: if `f : Œ± ‚Üí Œ≤` is a function defined on a
connected set `s` that takes both values `‚â§ a` and values `‚â• a` on `s`, then it is equal to `a` at
some point of `s`. We also prove that intervals in a dense conditionally complete order are
preconnected and any preconnected set is an interval. Then we specialize IVT to functions continuous
on intervals.

## Main results

* `IsPreconnected_I??` : all intervals `I??` are preconnected,
* `IsPreconnected.intermediate_value`, `intermediate_value_univ` : Intermediate Value Theorem for
  connected sets and connected spaces, respectively;
* `intermediate_value_Icc`, `intermediate_value_Icc'`: Intermediate Value Theorem for functions
  on closed intervals.

### Miscellaneous facts

* `IsClosed.Icc_subset_of_forall_mem_nhdsWithin` : ‚ÄúContinuous induction‚Äù principle;
  if `s ‚à© [a, b]` is closed, `a ‚àà s`, and for each `x ‚àà [a, b) ‚à© s` some of its right neighborhoods
  is included `s`, then `[a, b] ‚äÜ s`.
* `IsClosed.Icc_subset_of_forall_exists_gt`, `IsClosed.mem_of_ge_of_forall_exists_gt` : two
  other versions of the ‚Äúcontinuous induction‚Äù principle.
* `ContinuousOn.StrictMonoOn_of_InjOn_Ioo` :
  Every continuous injective `f : (a, b) ‚Üí Œ¥` is strictly monotone
  or antitone (increasing or decreasing).

## Tags

intermediate value theorem, connected space, connected set
-/


open Filter OrderDual TopologicalSpace Function Set
open scoped Topology Filter Interval

universe u v

/-!
### Intermediate value theorem on a (pre)connected space

In this section we prove the following theorem (see `IsPreconnected.intermediate_value‚ÇÇ`): if `f`
and `g` are two functions continuous on a preconnected set `s`, `f a ‚â§ g a` at some `a ‚àà s` and
`g b ‚â§ f b` at some `b ‚àà s`, then `f c = g c` at some `c ‚àà s`. We prove several versions of this
statement, including the classical IVT that corresponds to a constant function `g`.
-/

section

variable {X : Type u} {Œ± : Type v} [TopologicalSpace X] [LinearOrder Œ±] [TopologicalSpace Œ±]
  [OrderClosedTopology Œ±]

/-- Intermediate value theorem for two functions: if `f` and `g` are two continuous functions
on a preconnected space and `f a ‚â§ g a` and `g b ‚â§ f b`, then for some `x` we have `f x = g x`. -/
theorem intermediate_value_univ‚ÇÇ [PreconnectedSpace X] {a b : X} {f g : X ‚Üí Œ±} (hf : Continuous f)
    (hg : Continuous g) (ha : f a ‚â§ g a) (hb : g b ‚â§ f b) : ‚àÉ x, f x = g x := by
  obtain ‚ü®x, _, hfg, hgf‚ü© : (univ ‚à© { x | f x ‚â§ g x ‚àß g x ‚â§ f x }).Nonempty :=
    isPreconnected_closed_iff.1 PreconnectedSpace.isPreconnected_univ _ _ (isClosed_le hf hg)
      (isClosed_le hg hf) (fun _ _ => le_total _ _) ‚ü®a, trivial, ha‚ü© ‚ü®b, trivial, hb‚ü©
  exact ‚ü®x, le_antisymm hfg hgf‚ü©

theorem intermediate_value_univ‚ÇÇ_eventually‚ÇÅ [PreconnectedSpace X] {a : X} {l : Filter X} [NeBot l]
    {f g : X ‚Üí Œ±} (hf : Continuous f) (hg : Continuous g) (ha : f a ‚â§ g a) (he : g ‚â§·∂†[l] f) :
    ‚àÉ x, f x = g x :=
  let ‚ü®_, h‚ü© := he.exists; intermediate_value_univ‚ÇÇ hf hg ha h

theorem intermediate_value_univ‚ÇÇ_eventually‚ÇÇ [PreconnectedSpace X] {l‚ÇÅ l‚ÇÇ : Filter X} [NeBot l‚ÇÅ]
    [NeBot l‚ÇÇ] {f g : X ‚Üí Œ±} (hf : Continuous f) (hg : Continuous g) (he‚ÇÅ : f ‚â§·∂†[l‚ÇÅ] g)
    (he‚ÇÇ : g ‚â§·∂†[l‚ÇÇ] f) : ‚àÉ x, f x = g x :=
  let ‚ü®_, h‚ÇÅ‚ü© := he‚ÇÅ.exists
  let ‚ü®_, h‚ÇÇ‚ü© := he‚ÇÇ.exists
  intermediate_value_univ‚ÇÇ hf hg h‚ÇÅ h‚ÇÇ

/-- Intermediate value theorem for two functions: if `f` and `g` are two functions continuous
on a preconnected set `s` and for some `a b ‚àà s` we have `f a ‚â§ g a` and `g b ‚â§ f b`,
then for some `x ‚àà s` we have `f x = g x`. -/
theorem IsPreconnected.intermediate_value‚ÇÇ {s : Set X} (hs : IsPreconnected s) {a b : X}
    (ha : a ‚àà s) (hb : b ‚àà s) {f g : X ‚Üí Œ±} (hf : ContinuousOn f s) (hg : ContinuousOn g s)
    (ha' : f a ‚â§ g a) (hb' : g b ‚â§ f b) : ‚àÉ x ‚àà s, f x = g x :=
  let ‚ü®x, hx‚ü© :=
    @intermediate_value_univ‚ÇÇ s Œ± _ _ _ _ (Subtype.preconnectedSpace hs) ‚ü®a, ha‚ü© ‚ü®b, hb‚ü© _ _
      (continuousOn_iff_continuous_restrict.1 hf) (continuousOn_iff_continuous_restrict.1 hg) ha'
      hb'
  ‚ü®x, x.2, hx‚ü©

theorem IsPreconnected.intermediate_value‚ÇÇ_eventually‚ÇÅ {s : Set X} (hs : IsPreconnected s) {a : X}
    {l : Filter X} (ha : a ‚àà s) [NeBot l] (hl : l ‚â§ ùìü s) {f g : X ‚Üí Œ±} (hf : ContinuousOn f s)
    (hg : ContinuousOn g s) (ha' : f a ‚â§ g a) (he : g ‚â§·∂†[l] f) : ‚àÉ x ‚àà s, f x = g x := by
  rw [continuousOn_iff_continuous_restrict] at hf hg
  obtain ‚ü®b, h‚ü© :=
    @intermediate_value_univ‚ÇÇ_eventually‚ÇÅ _ _ _ _ _ _ (Subtype.preconnectedSpace hs) ‚ü®a, ha‚ü© _
      (comap_coe_neBot_of_le_principal hl) _ _ hf hg ha' (he.comap _)
  exact ‚ü®b, b.prop, h‚ü©

theorem IsPreconnected.intermediate_value‚ÇÇ_eventually‚ÇÇ {s : Set X} (hs : IsPreconnected s)
    {l‚ÇÅ l‚ÇÇ : Filter X} [NeBot l‚ÇÅ] [NeBot l‚ÇÇ] (hl‚ÇÅ : l‚ÇÅ ‚â§ ùìü s) (hl‚ÇÇ : l‚ÇÇ ‚â§ ùìü s) {f g : X ‚Üí Œ±}
    (hf : ContinuousOn f s) (hg : ContinuousOn g s) (he‚ÇÅ : f ‚â§·∂†[l‚ÇÅ] g) (he‚ÇÇ : g ‚â§·∂†[l‚ÇÇ] f) :
    ‚àÉ x ‚àà s, f x = g x := by
  rw [continuousOn_iff_continuous_restrict] at hf hg
  obtain ‚ü®b, h‚ü© :=
    @intermediate_value_univ‚ÇÇ_eventually‚ÇÇ _ _ _ _ _ _ (Subtype.preconnectedSpace hs) _ _
      (comap_coe_neBot_of_le_principal hl‚ÇÅ) (comap_coe_neBot_of_le_principal hl‚ÇÇ) _ _ hf hg
      (he‚ÇÅ.comap _) (he‚ÇÇ.comap _)
  exact ‚ü®b, b.prop, h‚ü©

/-- **Intermediate Value Theorem** for continuous functions on connected sets. -/
theorem IsPreconnected.intermediate_value {s : Set X} (hs : IsPreconnected s) {a b : X} (ha : a ‚àà s)
    (hb : b ‚àà s) {f : X ‚Üí Œ±} (hf : ContinuousOn f s) : Icc (f a) (f b) ‚äÜ f '' s := fun _x hx =>
  hs.intermediate_value‚ÇÇ ha hb hf continuousOn_const hx.1 hx.2

theorem IsPreconnected.intermediate_value_Ico {s : Set X} (hs : IsPreconnected s) {a : X}
    {l : Filter X} (ha : a ‚àà s) [NeBot l] (hl : l ‚â§ ùìü s) {f : X ‚Üí Œ±} (hf : ContinuousOn f s) {v : Œ±}
    (ht : Tendsto f l (ùìù v)) : Ico (f a) v ‚äÜ f '' s := fun _ h =>
  hs.intermediate_value‚ÇÇ_eventually‚ÇÅ ha hl hf continuousOn_const h.1 (ht.eventually_const_le h.2)

theorem IsPreconnected.intermediate_value_Ioc {s : Set X} (hs : IsPreconnected s) {a : X}
    {l : Filter X} (ha : a ‚àà s) [NeBot l] (hl : l ‚â§ ùìü s) {f : X ‚Üí Œ±} (hf : ContinuousOn f s) {v : Œ±}
    (ht : Tendsto f l (ùìù v)) : Ioc v (f a) ‚äÜ f '' s := fun _ h =>
  (hs.intermediate_value‚ÇÇ_eventually‚ÇÅ ha hl continuousOn_const hf h.2
    (ht.eventually_le_const h.1)).imp fun _ h => h.imp_right Eq.symm

theorem IsPreconnected.intermediate_value_Ioo {s : Set X} (hs : IsPreconnected s) {l‚ÇÅ l‚ÇÇ : Filter X}
    [NeBot l‚ÇÅ] [NeBot l‚ÇÇ] (hl‚ÇÅ : l‚ÇÅ ‚â§ ùìü s) (hl‚ÇÇ : l‚ÇÇ ‚â§ ùìü s) {f : X ‚Üí Œ±} (hf : ContinuousOn f s)
    {v‚ÇÅ v‚ÇÇ : Œ±} (ht‚ÇÅ : Tendsto f l‚ÇÅ (ùìù v‚ÇÅ)) (ht‚ÇÇ : Tendsto f l‚ÇÇ (ùìù v‚ÇÇ)) :
    Ioo v‚ÇÅ v‚ÇÇ ‚äÜ f '' s := fun _ h =>
  hs.intermediate_value‚ÇÇ_eventually‚ÇÇ hl‚ÇÅ hl‚ÇÇ hf continuousOn_const
    (ht‚ÇÅ.eventually_le_const h.1) (ht‚ÇÇ.eventually_const_le h.2)

theorem IsPreconnected.intermediate_value_Ici {s : Set X} (hs : IsPreconnected s) {a : X}
    {l : Filter X} (ha : a ‚àà s) [NeBot l] (hl : l ‚â§ ùìü s) {f : X ‚Üí Œ±} (hf : ContinuousOn f s)
    (ht : Tendsto f l atTop) : Ici (f a) ‚äÜ f '' s := fun y h =>
  hs.intermediate_value‚ÇÇ_eventually‚ÇÅ ha hl hf continuousOn_const h (tendsto_atTop.1 ht y)

theorem IsPreconnected.intermediate_value_Iic {s : Set X} (hs : IsPreconnected s) {a : X}
    {l : Filter X} (ha : a ‚àà s) [NeBot l] (hl : l ‚â§ ùìü s) {f : X ‚Üí Œ±} (hf : ContinuousOn f s)
    (ht : Tendsto f l atBot) : Iic (f a) ‚äÜ f '' s := fun y h =>
  (hs.intermediate_value‚ÇÇ_eventually‚ÇÅ ha hl continuousOn_const hf h (tendsto_atBot.1 ht y)).imp
    fun _ h => h.imp_right Eq.symm

theorem IsPreconnected.intermediate_value_Ioi {s : Set X} (hs : IsPreconnected s) {l‚ÇÅ l‚ÇÇ : Filter X}
    [NeBot l‚ÇÅ] [NeBot l‚ÇÇ] (hl‚ÇÅ : l‚ÇÅ ‚â§ ùìü s) (hl‚ÇÇ : l‚ÇÇ ‚â§ ùìü s) {f : X ‚Üí Œ±} (hf : ContinuousOn f s)
    {v : Œ±} (ht‚ÇÅ : Tendsto f l‚ÇÅ (ùìù v)) (ht‚ÇÇ : Tendsto f l‚ÇÇ atTop) : Ioi v ‚äÜ f '' s := fun y h =>
  hs.intermediate_value‚ÇÇ_eventually‚ÇÇ hl‚ÇÅ hl‚ÇÇ hf continuousOn_const
    (ht‚ÇÅ.eventually_le_const h) (ht‚ÇÇ.eventually_ge_atTop y)

theorem IsPreconnected.intermediate_value_Iio {s : Set X} (hs : IsPreconnected s) {l‚ÇÅ l‚ÇÇ : Filter X}
    [NeBot l‚ÇÅ] [NeBot l‚ÇÇ] (hl‚ÇÅ : l‚ÇÅ ‚â§ ùìü s) (hl‚ÇÇ : l‚ÇÇ ‚â§ ùìü s) {f : X ‚Üí Œ±} (hf : ContinuousOn f s)
    {v : Œ±} (ht‚ÇÅ : Tendsto f l‚ÇÅ atBot) (ht‚ÇÇ : Tendsto f l‚ÇÇ (ùìù v)) : Iio v ‚äÜ f '' s := fun y h =>
  hs.intermediate_value‚ÇÇ_eventually‚ÇÇ hl‚ÇÅ hl‚ÇÇ hf continuousOn_const (ht‚ÇÅ.eventually_le_atBot y)
    (ht‚ÇÇ.eventually_const_le h)

theorem IsPreconnected.intermediate_value_Iii {s : Set X} (hs : IsPreconnected s) {l‚ÇÅ l‚ÇÇ : Filter X}
    [NeBot l‚ÇÅ] [NeBot l‚ÇÇ] (hl‚ÇÅ : l‚ÇÅ ‚â§ ùìü s) (hl‚ÇÇ : l‚ÇÇ ‚â§ ùìü s) {f : X ‚Üí Œ±} (hf : ContinuousOn f s)
    (ht‚ÇÅ : Tendsto f l‚ÇÅ atBot) (ht‚ÇÇ : Tendsto f l‚ÇÇ atTop) : univ ‚äÜ f '' s := fun y _ =>
  hs.intermediate_value‚ÇÇ_eventually‚ÇÇ hl‚ÇÅ hl‚ÇÇ hf continuousOn_const (ht‚ÇÅ.eventually_le_atBot y)
    (ht‚ÇÇ.eventually_ge_atTop y)

/-- **Intermediate Value Theorem** for continuous functions on connected spaces. -/
theorem intermediate_value_univ [PreconnectedSpace X] (a b : X) {f : X ‚Üí Œ±} (hf : Continuous f) :
    Icc (f a) (f b) ‚äÜ range f := fun _ hx => intermediate_value_univ‚ÇÇ hf continuous_const hx.1 hx.2

/-- **Intermediate Value Theorem** for continuous functions on connected spaces. -/
theorem mem_range_of_exists_le_of_exists_ge [PreconnectedSpace X] {c : Œ±} {f : X ‚Üí Œ±}
    (hf : Continuous f) (h‚ÇÅ : ‚àÉ a, f a ‚â§ c) (h‚ÇÇ : ‚àÉ b, c ‚â§ f b) : c ‚àà range f :=
  let ‚ü®a, ha‚ü© := h‚ÇÅ; let ‚ü®b, hb‚ü© := h‚ÇÇ; intermediate_value_univ a b hf ‚ü®ha, hb‚ü©

/-!
### (Pre)connected sets in a linear order

In this section we prove the following results:

* `IsPreconnected.ordConnected`: any preconnected set `s` in a linear order is `OrdConnected`,
  i.e. `a ‚àà s` and `b ‚àà s` imply `Icc a b ‚äÜ s`;

* `IsPreconnected.mem_intervals`: any preconnected set `s` in a conditionally complete linear order
  is one of the intervals `Set.Icc`, `set.`Ico`, `set.Ioc`, `set.Ioo`, ``Set.Ici`, `Set.Iic`,
  `Set.Ioi`, `Set.Iio`; note that this is false for non-complete orders: e.g., in `‚Ñù \ {0}`, the set
  of positive numbers cannot be represented as `Set.Ioi _`.

-/


/-- If a preconnected set contains endpoints of an interval, then it includes the whole interval. -/
theorem IsPreconnected.Icc_subset {s : Set Œ±} (hs : IsPreconnected s) {a b : Œ±} (ha : a ‚àà s)
    (hb : b ‚àà s) : Icc a b ‚äÜ s := by
  simpa only [image_id] using hs.intermediate_value ha hb continuousOn_id

theorem IsPreconnected.ordConnected {s : Set Œ±} (h : IsPreconnected s) : OrdConnected s :=
  ‚ü®fun _ hx _ hy => h.Icc_subset hx hy‚ü©

/-- If a preconnected set contains endpoints of an interval, then it includes the whole interval. -/
theorem IsConnected.Icc_subset {s : Set Œ±} (hs : IsConnected s) {a b : Œ±} (ha : a ‚àà s)
    (hb : b ‚àà s) : Icc a b ‚äÜ s :=
  hs.2.Icc_subset ha hb

/-- If preconnected set in a linear order space is unbounded below and above, then it is the whole
space. -/
theorem IsPreconnected.eq_univ_of_unbounded {s : Set Œ±} (hs : IsPreconnected s) (hb : ¬¨BddBelow s)
    (ha : ¬¨BddAbove s) : s = univ := by
  refine eq_univ_of_forall fun x => ?_
  obtain ‚ü®y, ys, hy‚ü© : ‚àÉ y ‚àà s, y < x := not_bddBelow_iff.1 hb x
  obtain ‚ü®z, zs, hz‚ü© : ‚àÉ z ‚àà s, x < z := not_bddAbove_iff.1 ha x
  exact hs.Icc_subset ys zs ‚ü®le_of_lt hy, le_of_lt hz‚ü©

end

variable {Œ± : Type u} [ConditionallyCompleteLinearOrder Œ±] [TopologicalSpace Œ±] [OrderTopology Œ±]

/-- A bounded connected subset of a conditionally complete linear order includes the open interval
`(Inf s, Sup s)`. -/
theorem IsConnected.Ioo_csInf_csSup_subset {s : Set Œ±} (hs : IsConnected s) (hb : BddBelow s)
    (ha : BddAbove s) : Ioo (sInf s) (sSup s) ‚äÜ s := fun _x hx =>
  let ‚ü®_y, ys, hy‚ü© := (isGLB_lt_iff (isGLB_csInf hs.nonempty hb)).1 hx.1
  let ‚ü®_z, zs, hz‚ü© := (lt_isLUB_iff (isLUB_csSup hs.nonempty ha)).1 hx.2
  hs.Icc_subset ys zs ‚ü®hy.le, hz.le‚ü©

theorem eq_Icc_csInf_csSup_of_connected_bdd_closed {s : Set Œ±} (hc : IsConnected s)
    (hb : BddBelow s) (ha : BddAbove s) (hcl : IsClosed s) : s = Icc (sInf s) (sSup s) :=
  (subset_Icc_csInf_csSup hb ha).antisymm <|
    hc.Icc_subset (hcl.csInf_mem hc.nonempty hb) (hcl.csSup_mem hc.nonempty ha)

theorem IsPreconnected.Ioi_csInf_subset {s : Set Œ±} (hs : IsPreconnected s) (hb : BddBelow s)
    (ha : ¬¨BddAbove s) : Ioi (sInf s) ‚äÜ s := fun x hx =>
  have sne : s.Nonempty := nonempty_of_not_bddAbove ha
  let ‚ü®_y, ys, hy‚ü© : ‚àÉ y ‚àà s, y < x := (isGLB_lt_iff (isGLB_csInf sne hb)).1 hx
  let ‚ü®_z, zs, hz‚ü© : ‚àÉ z ‚àà s, x < z := not_bddAbove_iff.1 ha x
  hs.Icc_subset ys zs ‚ü®hy.le, hz.le‚ü©

theorem IsPreconnected.Iio_csSup_subset {s : Set Œ±} (hs : IsPreconnected s) (hb : ¬¨BddBelow s)
    (ha : BddAbove s) : Iio (sSup s) ‚äÜ s :=
  IsPreconnected.Ioi_csInf_subset (Œ± := Œ±·µí·µà) hs ha hb

/-- A preconnected set in a conditionally complete linear order is either one of the intervals
`[Inf s, Sup s]`, `[Inf s, Sup s)`, `(Inf s, Sup s]`, `(Inf s, Sup s)`, `[Inf s, +‚àû)`,
`(Inf s, +‚àû)`, `(-‚àû, Sup s]`, `(-‚àû, Sup s)`, `(-‚àû, +‚àû)`, or `‚àÖ`. The converse statement requires
`Œ±` to be densely ordered. -/
theorem IsPreconnected.mem_intervals {s : Set Œ±} (hs : IsPreconnected s) :
    s ‚àà
      ({Icc (sInf s) (sSup s), Ico (sInf s) (sSup s), Ioc (sInf s) (sSup s), Ioo (sInf s) (sSup s),
          Ici (sInf s), Ioi (sInf s), Iic (sSup s), Iio (sSup s), univ, ‚àÖ} : Set (Set Œ±)) := by
  rcases s.eq_empty_or_nonempty with (rfl | hne)
  ¬∑ apply_rules [Or.inr, mem_singleton]
  have hs' : IsConnected s := ‚ü®hne, hs‚ü©
  by_cases hb : BddBelow s <;> by_cases ha : BddAbove s
  ¬∑ refine mem_of_subset_of_mem ?_ <| mem_Icc_Ico_Ioc_Ioo_of_subset_of_subset
      (hs'.Ioo_csInf_csSup_subset hb ha) (subset_Icc_csInf_csSup hb ha)
    simp only [insert_subset_iff, mem_insert_iff, mem_singleton_iff, true_or, or_true,
      singleton_subset_iff, and_self]
  ¬∑ refine Or.inr <| Or.inr <| Or.inr <| Or.inr ?_
    rcases mem_Ici_Ioi_of_subset_of_subset (hs.Ioi_csInf_subset hb ha) fun x hx ‚Ü¶
      csInf_le hb hx with hs | hs
    ¬∑ exact Or.inl hs
    ¬∑ exact Or.inr (Or.inl hs)
  ¬∑ iterate 6 apply Or.inr
    rcases mem_Iic_Iio_of_subset_of_subset (hs.Iio_csSup_subset hb ha) fun x hx ‚Ü¶
      le_csSup ha hx with hs | hs
    ¬∑ exact Or.inl hs
    ¬∑ exact Or.inr (Or.inl hs)
  ¬∑ iterate 8 apply Or.inr
    exact Or.inl (hs.eq_univ_of_unbounded hb ha)

/-- A preconnected set is either one of the intervals `Icc`, `Ico`, `Ioc`, `Ioo`, `Ici`, `Ioi`,
`Iic`, `Iio`, or `univ`, or `‚àÖ`. The converse statement requires `Œ±` to be densely ordered. Though
one can represent `‚àÖ` as `(Inf ‚àÖ, Inf ‚àÖ)`, we include it into the list of possible cases to improve
readability. -/
theorem setOf_isPreconnected_subset_of_ordered :
    { s : Set Œ± | IsPreconnected s } ‚äÜ
      -- bounded intervals
      (range (uncurry Icc) ‚à™ range (uncurry Ico) ‚à™ range (uncurry Ioc) ‚à™ range (uncurry Ioo)) ‚à™
      -- unbounded intervals and `univ`
      (range Ici ‚à™ range Ioi ‚à™ range Iic ‚à™ range Iio ‚à™ {univ, ‚àÖ}) := by
  intro s hs
  rcases hs.mem_intervals with (hs | hs | hs | hs | hs | hs | hs | hs | hs | hs) <;> rw [hs] <;>
    simp only [union_insert, union_singleton, mem_insert_iff, mem_union, mem_range, Prod.exists,
      uncurry_apply_pair, exists_apply_eq_apply, true_or, or_true, exists_apply_eq_apply2]

/-!
### Intervals are connected

In this section we prove that a closed interval (hence, any `OrdConnected` set) in a dense
conditionally complete linear order is preconnected.
-/


/-- A "continuous induction principle" for a closed interval: if a set `s` meets `[a, b]`
on a closed subset, contains `a`, and the set `s ‚à© [a, b)` has no maximal point, then `b ‚àà s`. -/
theorem IsClosed.mem_of_ge_of_forall_exists_gt {a b : Œ±} {s : Set Œ±} (hs : IsClosed (s ‚à© Icc a b))
    (ha : a ‚àà s) (hab : a ‚â§ b) (hgt : ‚àÄ x ‚àà s ‚à© Ico a b, (s ‚à© Ioc x b).Nonempty) : b ‚àà s := by
  let S := s ‚à© Icc a b
  replace ha : a ‚àà S := ‚ü®ha, left_mem_Icc.2 hab‚ü©
  have Sbd : BddAbove S := ‚ü®b, fun z hz => hz.2.2‚ü©
  let c := sSup (s ‚à© Icc a b)
  have c_mem : c ‚àà S := hs.csSup_mem ‚ü®_, ha‚ü© Sbd
  have c_le : c ‚â§ b := csSup_le ‚ü®_, ha‚ü© fun x hx => hx.2.2
  rcases eq_or_lt_of_le c_le with hc | hc
  ¬∑ exact hc ‚ñ∏ c_mem.1
  exfalso
  rcases hgt c ‚ü®c_mem.1, c_mem.2.1, hc‚ü© with ‚ü®x, xs, cx, xb‚ü©
  exact not_lt_of_ge (le_csSup Sbd ‚ü®xs, le_trans (le_csSup Sbd ha) (le_of_lt cx), xb‚ü©) cx

/-- A "continuous induction principle" for a closed interval: if a set `s` meets `[a, b]`
on a closed subset, contains `a`, and for any `a ‚â§ x < y ‚â§ b`, `x ‚àà s`, the set `s ‚à© (x, y]`
is not empty, then `[a, b] ‚äÜ s`. -/
theorem IsClosed.Icc_subset_of_forall_exists_gt {a b : Œ±} {s : Set Œ±} (hs : IsClosed (s ‚à© Icc a b))
    (ha : a ‚àà s) (hgt : ‚àÄ x ‚àà s ‚à© Ico a b, ‚àÄ y ‚àà Ioi x, (s ‚à© Ioc x y).Nonempty) : Icc a b ‚äÜ s := by
  intro y hy
  have : IsClosed (s ‚à© Icc a y) := by
    suffices s ‚à© Icc a y = s ‚à© Icc a b ‚à© Icc a y by
      rw [this]
      exact IsClosed.inter hs isClosed_Icc
    rw [inter_assoc]
    congr
    exact (inter_eq_self_of_subset_right <| Icc_subset_Icc_right hy.2).symm
  exact
    IsClosed.mem_of_ge_of_forall_exists_gt this ha hy.1 fun x hx =>
      hgt x ‚ü®hx.1, Ico_subset_Ico_right hy.2 hx.2‚ü© y hx.2.2

variable [DenselyOrdered Œ±] {a b : Œ±}

/-- A "continuous induction principle" for a closed interval: if a set `s` meets `[a, b]`
on a closed subset, contains `a`, and for any `x ‚àà s ‚à© [a, b)` the set `s` includes some open
neighborhood of `x` within `(x, +‚àû)`, then `[a, b] ‚äÜ s`. -/
theorem IsClosed.Icc_subset_of_forall_mem_nhdsWithin {a b : Œ±} {s : Set Œ±}
    (hs : IsClosed (s ‚à© Icc a b)) (ha : a ‚àà s) (hgt : ‚àÄ x ‚àà s ‚à© Ico a b, s ‚àà ùìù[>] x) :
    Icc a b ‚äÜ s := by
  apply hs.Icc_subset_of_forall_exists_gt ha
  rintro x ‚ü®hxs, hxab‚ü© y hyxb
  have : s ‚à© Ioc x y ‚àà ùìù[>] x := inter_mem (hgt x ‚ü®hxs, hxab‚ü©) (Ioc_mem_nhdsGT hyxb)
  exact (nhdsGT_neBot_of_exists_gt ‚ü®b, hxab.2‚ü©).nonempty_of_mem this

theorem isPreconnected_Icc_aux (x y : Œ±) (s t : Set Œ±) (hxy : x ‚â§ y) (hs : IsClosed s)
    (ht : IsClosed t) (hab : Icc a b ‚äÜ s ‚à™ t) (hx : x ‚àà Icc a b ‚à© s) (hy : y ‚àà Icc a b ‚à© t) :
    (Icc a b ‚à© (s ‚à© t)).Nonempty := by
  have xyab : Icc x y ‚äÜ Icc a b := Icc_subset_Icc hx.1.1 hy.1.2
  by_contra hst
  suffices Icc x y ‚äÜ s from
    hst ‚ü®y, xyab <| right_mem_Icc.2 hxy, this <| right_mem_Icc.2 hxy, hy.2‚ü©
  apply (IsClosed.inter hs isClosed_Icc).Icc_subset_of_forall_mem_nhdsWithin hx.2
  rintro z ‚ü®zs, hz‚ü©
  have zt : z ‚àà t·∂ú := fun zt => hst ‚ü®z, xyab <| Ico_subset_Icc_self hz, zs, zt‚ü©
  have : t·∂ú ‚à© Ioc z y ‚àà ùìù[>] z := by
    rw [‚Üê nhdsWithin_Ioc_eq_nhdsGT hz.2]
    exact mem_nhdsWithin.2 ‚ü®t·∂ú, ht.isOpen_compl, zt, Subset.rfl‚ü©
  apply mem_of_superset this
  have : Ioc z y ‚äÜ s ‚à™ t := fun w hw => hab (xyab ‚ü®le_trans hz.1 (le_of_lt hw.1), hw.2‚ü©)
  exact fun w ‚ü®wt, wzy‚ü© => (this wzy).elim id fun h => (wt h).elim

/-- A closed interval in a densely ordered conditionally complete linear order is preconnected. -/
theorem isPreconnected_Icc : IsPreconnected (Icc a b) :=
  isPreconnected_closed_iff.2
    (by
      rintro s t hs ht hab ‚ü®x, hx‚ü© ‚ü®y, hy‚ü©
      -- This used to use `wlog`, but it was causing timeouts.
      rcases le_total x y with h | h
      ¬∑ exact isPreconnected_Icc_aux x y s t h hs ht hab hx hy
      ¬∑ rw [inter_comm s t]
        rw [union_comm s t] at hab
        exact isPreconnected_Icc_aux y x t s h ht hs hab hy hx)

theorem isPreconnected_uIcc : IsPreconnected ([[a, b]]) :=
  isPreconnected_Icc

theorem Set.OrdConnected.isPreconnected {s : Set Œ±} (h : s.OrdConnected) : IsPreconnected s :=
  isPreconnected_of_forall_pair fun x hx y hy =>
    ‚ü®[[x, y]], h.uIcc_subset hx hy, left_mem_uIcc, right_mem_uIcc, isPreconnected_uIcc‚ü©

theorem isPreconnected_iff_ordConnected {s : Set Œ±} : IsPreconnected s ‚Üî OrdConnected s :=
  ‚ü®IsPreconnected.ordConnected, Set.OrdConnected.isPreconnected‚ü©

theorem isPreconnected_Ici : IsPreconnected (Ici a) :=
  ordConnected_Ici.isPreconnected

theorem isPreconnected_Iic : IsPreconnected (Iic a) :=
  ordConnected_Iic.isPreconnected

theorem isPreconnected_Iio : IsPreconnected (Iio a) :=
  ordConnected_Iio.isPreconnected

theorem isPreconnected_Ioi : IsPreconnected (Ioi a) :=
  ordConnected_Ioi.isPreconnected

theorem isPreconnected_Ioo : IsPreconnected (Ioo a b) :=
  ordConnected_Ioo.isPreconnected

theorem isPreconnected_Ioc : IsPreconnected (Ioc a b) :=
  ordConnected_Ioc.isPreconnected

theorem isPreconnected_Ico : IsPreconnected (Ico a b) :=
  ordConnected_Ico.isPreconnected

theorem isConnected_Ici : IsConnected (Ici a) :=
  ‚ü®nonempty_Ici, isPreconnected_Ici‚ü©

theorem isConnected_Iic : IsConnected (Iic a) :=
  ‚ü®nonempty_Iic, isPreconnected_Iic‚ü©

theorem isConnected_Ioi [NoMaxOrder Œ±] : IsConnected (Ioi a) :=
  ‚ü®nonempty_Ioi, isPreconnected_Ioi‚ü©

theorem isConnected_Iio [NoMinOrder Œ±] : IsConnected (Iio a) :=
  ‚ü®nonempty_Iio, isPreconnected_Iio‚ü©

theorem isConnected_Icc (h : a ‚â§ b) : IsConnected (Icc a b) :=
  ‚ü®nonempty_Icc.2 h, isPreconnected_Icc‚ü©

theorem isConnected_Ioo (h : a < b) : IsConnected (Ioo a b) :=
  ‚ü®nonempty_Ioo.2 h, isPreconnected_Ioo‚ü©

theorem isConnected_Ioc (h : a < b) : IsConnected (Ioc a b) :=
  ‚ü®nonempty_Ioc.2 h, isPreconnected_Ioc‚ü©

theorem isConnected_Ico (h : a < b) : IsConnected (Ico a b) :=
  ‚ü®nonempty_Ico.2 h, isPreconnected_Ico‚ü©

instance (priority := 100) ordered_connected_space : PreconnectedSpace Œ± :=
  ‚ü®ordConnected_univ.isPreconnected‚ü©

/-- In a dense conditionally complete linear order, the set of preconnected sets is exactly
the set of the intervals `Icc`, `Ico`, `Ioc`, `Ioo`, `Ici`, `Ioi`, `Iic`, `Iio`, `(-‚àû, +‚àû)`,
or `‚àÖ`. Though one can represent `‚àÖ` as `(sInf s, sInf s)`, we include it into the list of
possible cases to improve readability. -/
theorem setOf_isPreconnected_eq_of_ordered :
    { s : Set Œ± | IsPreconnected s } =
      -- bounded intervals
      range (uncurry Icc) ‚à™ range (uncurry Ico) ‚à™ range (uncurry Ioc) ‚à™ range (uncurry Ioo) ‚à™
      -- unbounded intervals and `univ`
      (range Ici ‚à™ range Ioi ‚à™ range Iic ‚à™ range Iio ‚à™ {univ, ‚àÖ}) := by
  refine Subset.antisymm setOf_isPreconnected_subset_of_ordered ?_
  simp only [subset_def, forall_mem_range, uncurry, or_imp, forall_and, mem_union,
    mem_setOf_eq, insert_eq, mem_singleton_iff, forall_eq, forall_true_iff, and_true,
    isPreconnected_Icc, isPreconnected_Ico, isPreconnected_Ioc, isPreconnected_Ioo,
    isPreconnected_Ioi, isPreconnected_Iio, isPreconnected_Ici, isPreconnected_Iic,
    isPreconnected_univ, isPreconnected_empty]

/-- This lemmas characterizes when a subset `s` of a densely ordered conditionally complete linear
order is totally disconnected with respect to the order topology: between any two distinct points
of `s` must lie a point not in `s`. -/
lemma isTotallyDisconnected_iff_lt {s : Set Œ±} :
    IsTotallyDisconnected s ‚Üî ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, x < y ‚Üí ‚àÉ z ‚àâ s, z ‚àà Ioo x y := by
  simp only [IsTotallyDisconnected, isPreconnected_iff_ordConnected, ‚Üê not_nontrivial_iff,
    nontrivial_iff_exists_lt, not_exists, not_and]
  refine ‚ü®fun h x hx y hy hxy ‚Ü¶ ?_, fun h t hts ht x hx y hy hxy ‚Ü¶ ?_‚ü©
  ¬∑ simp_rw [‚Üê not_ordConnected_inter_Icc_iff hx hy]
    exact fun hs ‚Ü¶ h _ inter_subset_left hs _ ‚ü®hx, le_rfl, hxy.le‚ü© _ ‚ü®hy, hxy.le, le_rfl‚ü© hxy
  ¬∑ obtain ‚ü®z, h1z, h2z‚ü© := h x (hts hx) y (hts hy) hxy
    exact h1z <| hts <| ht.1 hx hy ‚ü®h2z.1.le, h2z.2.le‚ü©

/-!
### Intermediate Value Theorem on an interval

In this section we prove several versions of the Intermediate Value Theorem for a function
continuous on an interval.
-/


variable {Œ¥ : Type*} [LinearOrder Œ¥] [TopologicalSpace Œ¥] [OrderClosedTopology Œ¥]

/-- **Intermediate Value Theorem** for continuous functions on closed intervals, case
`f a ‚â§ t ‚â§ f b`. -/
theorem intermediate_value_Icc {a b : Œ±} (hab : a ‚â§ b) {f : Œ± ‚Üí Œ¥} (hf : ContinuousOn f (Icc a b)) :
    Icc (f a) (f b) ‚äÜ f '' Icc a b :=
  isPreconnected_Icc.intermediate_value (left_mem_Icc.2 hab) (right_mem_Icc.2 hab) hf

/-- **Intermediate Value Theorem** for continuous functions on closed intervals, case
`f a ‚â• t ‚â• f b`. -/
theorem intermediate_value_Icc' {a b : Œ±} (hab : a ‚â§ b) {f : Œ± ‚Üí Œ¥}
    (hf : ContinuousOn f (Icc a b)) : Icc (f b) (f a) ‚äÜ f '' Icc a b :=
  isPreconnected_Icc.intermediate_value (right_mem_Icc.2 hab) (left_mem_Icc.2 hab) hf

/-- **Intermediate Value Theorem** for continuous functions on closed intervals, unordered case. -/
theorem intermediate_value_uIcc {a b : Œ±} {f : Œ± ‚Üí Œ¥} (hf : ContinuousOn f [[a, b]]) :
    [[f a, f b]] ‚äÜ f '' uIcc a b := by
  cases le_total (f a) (f b) <;> simp [*, isPreconnected_uIcc.intermediate_value]

/-- If `f : Œ± ‚Üí Œ±` is continuous on `[[a, b]]`, `a ‚â§ f a`, and `f b ‚â§ b`,
then `f` has a fixed point on `[[a, b]]`. -/
theorem exists_mem_uIcc_isFixedPt {a b : Œ±} {f : Œ± ‚Üí Œ±} (hf : ContinuousOn f (uIcc a b))
    (ha : a ‚â§ f a) (hb : f b ‚â§ b) : ‚àÉ c ‚àà [[a, b]], IsFixedPt f c :=
  isPreconnected_uIcc.intermediate_value‚ÇÇ right_mem_uIcc left_mem_uIcc hf continuousOn_id hb ha

/-- If `f : Œ± ‚Üí Œ±` is continuous on `[a, b]`, `a ‚â§ b`, `a ‚â§ f a`, and `f b ‚â§ b`,
then `f` has a fixed point on `[a, b]`.

In particular, if `[a, b]` is forward-invariant under `f`,
then `f` has a fixed point on `[a, b]`, see `exists_mem_Icc_isFixedPt_of_mapsTo`. -/
theorem exists_mem_Icc_isFixedPt {a b : Œ±} {f : Œ± ‚Üí Œ±} (hf : ContinuousOn f (Icc a b))
    (hle : a ‚â§ b) (ha : a ‚â§ f a) (hb : f b ‚â§ b) : ‚àÉ c ‚àà Icc a b, IsFixedPt f c :=
  isPreconnected_Icc.intermediate_value‚ÇÇ
    (right_mem_Icc.2 hle) (left_mem_Icc.2 hle) hf continuousOn_id hb ha

/-- If a closed interval is forward-invariant under a continuous map `f : Œ± ‚Üí Œ±`,
then this map has a fixed point on this interval. -/
theorem exists_mem_Icc_isFixedPt_of_mapsTo {a b : Œ±} {f : Œ± ‚Üí Œ±} (hf : ContinuousOn f (Icc a b))
    (hle : a ‚â§ b) (hmaps : MapsTo f (Icc a b) (Icc a b)) : ‚àÉ c ‚àà Icc a b, IsFixedPt f c :=
  exists_mem_Icc_isFixedPt hf hle (hmaps <| left_mem_Icc.2 hle).1 (hmaps <| right_mem_Icc.2 hle).2

theorem intermediate_value_Ico {a b : Œ±} (hab : a ‚â§ b) {f : Œ± ‚Üí Œ¥} (hf : ContinuousOn f (Icc a b)) :
    Ico (f a) (f b) ‚äÜ f '' Ico a b :=
  Or.elim (eq_or_lt_of_le hab) (fun he _ h => absurd h.2 (not_lt_of_ge (he ‚ñ∏ h.1))) fun hlt =>
    @IsPreconnected.intermediate_value_Ico _ _ _ _ _ _ _ isPreconnected_Ico _ _ ‚ü®refl a, hlt‚ü©
      (right_nhdsWithin_Ico_neBot hlt) inf_le_right _ (hf.mono Ico_subset_Icc_self) _
      ((hf.continuousWithinAt ‚ü®hab, refl b‚ü©).mono Ico_subset_Icc_self)

theorem intermediate_value_Ico' {a b : Œ±} (hab : a ‚â§ b) {f : Œ± ‚Üí Œ¥}
    (hf : ContinuousOn f (Icc a b)) : Ioc (f b) (f a) ‚äÜ f '' Ico a b :=
  Or.elim (eq_or_lt_of_le hab) (fun he _ h => absurd h.1 (not_lt_of_ge (he ‚ñ∏ h.2))) fun hlt =>
    @IsPreconnected.intermediate_value_Ioc _ _ _ _ _ _ _ isPreconnected_Ico _ _ ‚ü®refl a, hlt‚ü©
      (right_nhdsWithin_Ico_neBot hlt) inf_le_right _ (hf.mono Ico_subset_Icc_self) _
      ((hf.continuousWithinAt ‚ü®hab, refl b‚ü©).mono Ico_subset_Icc_self)

theorem intermediate_value_Ioc {a b : Œ±} (hab : a ‚â§ b) {f : Œ± ‚Üí Œ¥} (hf : ContinuousOn f (Icc a b)) :
    Ioc (f a) (f b) ‚äÜ f '' Ioc a b :=
  Or.elim (eq_or_lt_of_le hab) (fun he _ h => absurd h.2 (not_le_of_gt (he ‚ñ∏ h.1))) fun hlt =>
    @IsPreconnected.intermediate_value_Ioc _ _ _ _ _ _ _ isPreconnected_Ioc _ _ ‚ü®hlt, refl b‚ü©
      (left_nhdsWithin_Ioc_neBot hlt) inf_le_right _ (hf.mono Ioc_subset_Icc_self) _
      ((hf.continuousWithinAt ‚ü®refl a, hab‚ü©).mono Ioc_subset_Icc_self)

theorem intermediate_value_Ioc' {a b : Œ±} (hab : a ‚â§ b) {f : Œ± ‚Üí Œ¥}
    (hf : ContinuousOn f (Icc a b)) : Ico (f b) (f a) ‚äÜ f '' Ioc a b :=
  Or.elim (eq_or_lt_of_le hab) (fun he _ h => absurd h.1 (not_le_of_gt (he ‚ñ∏ h.2))) fun hlt =>
    @IsPreconnected.intermediate_value_Ico _ _ _ _ _ _ _ isPreconnected_Ioc _ _ ‚ü®hlt, refl b‚ü©
      (left_nhdsWithin_Ioc_neBot hlt) inf_le_right _ (hf.mono Ioc_subset_Icc_self) _
      ((hf.continuousWithinAt ‚ü®refl a, hab‚ü©).mono Ioc_subset_Icc_self)

theorem intermediate_value_Ioo {a b : Œ±} (hab : a ‚â§ b) {f : Œ± ‚Üí Œ¥} (hf : ContinuousOn f (Icc a b)) :
    Ioo (f a) (f b) ‚äÜ f '' Ioo a b :=
  Or.elim (eq_or_lt_of_le hab) (fun he _ h => absurd h.2 (not_lt_of_gt (he ‚ñ∏ h.1))) fun hlt =>
    @IsPreconnected.intermediate_value_Ioo _ _ _ _ _ _ _ isPreconnected_Ioo _ _
      (left_nhdsWithin_Ioo_neBot hlt) (right_nhdsWithin_Ioo_neBot hlt) inf_le_right inf_le_right _
      (hf.mono Ioo_subset_Icc_self) _ _
      ((hf.continuousWithinAt ‚ü®refl a, hab‚ü©).mono Ioo_subset_Icc_self)
      ((hf.continuousWithinAt ‚ü®hab, refl b‚ü©).mono Ioo_subset_Icc_self)

theorem intermediate_value_Ioo' {a b : Œ±} (hab : a ‚â§ b) {f : Œ± ‚Üí Œ¥}
    (hf : ContinuousOn f (Icc a b)) : Ioo (f b) (f a) ‚äÜ f '' Ioo a b :=
  Or.elim (eq_or_lt_of_le hab) (fun he _ h => absurd h.1 (not_lt_of_gt (he ‚ñ∏ h.2))) fun hlt =>
    @IsPreconnected.intermediate_value_Ioo _ _ _ _ _ _ _ isPreconnected_Ioo _ _
      (right_nhdsWithin_Ioo_neBot hlt) (left_nhdsWithin_Ioo_neBot hlt) inf_le_right inf_le_right _
      (hf.mono Ioo_subset_Icc_self) _ _
      ((hf.continuousWithinAt ‚ü®hab, refl b‚ü©).mono Ioo_subset_Icc_self)
      ((hf.continuousWithinAt ‚ü®refl a, hab‚ü©).mono Ioo_subset_Icc_self)

/-- **Intermediate value theorem**: if `f` is continuous on an order-connected set `s` and `a`,
`b` are two points of this set, then `f` sends `s` to a superset of `Icc (f x) (f y)`. -/
theorem ContinuousOn.surjOn_Icc {s : Set Œ±} [hs : OrdConnected s] {f : Œ± ‚Üí Œ¥}
    (hf : ContinuousOn f s) {a b : Œ±} (ha : a ‚àà s) (hb : b ‚àà s) : SurjOn f s (Icc (f a) (f b)) :=
  hs.isPreconnected.intermediate_value ha hb hf

/-- **Intermediate value theorem**: if `f` is continuous on an order-connected set `s` and `a`,
`b` are two points of this set, then `f` sends `s` to a superset of `[f x, f y]`. -/
theorem ContinuousOn.surjOn_uIcc {s : Set Œ±} [hs : OrdConnected s] {f : Œ± ‚Üí Œ¥}
    (hf : ContinuousOn f s) {a b : Œ±} (ha : a ‚àà s) (hb : b ‚àà s) :
    SurjOn f s (uIcc (f a) (f b)) := by
  rcases le_total (f a) (f b) with hab | hab <;> simp [hf.surjOn_Icc, *]

/-- A continuous function which tendsto `Filter.atTop` along `Filter.atTop` and to `atBot` along
`at_bot` is surjective. -/
theorem Continuous.surjective {f : Œ± ‚Üí Œ¥} (hf : Continuous f) (h_top : Tendsto f atTop atTop)
    (h_bot : Tendsto f atBot atBot) : Function.Surjective f := fun p =>
  mem_range_of_exists_le_of_exists_ge hf (h_bot.eventually (eventually_le_atBot p)).exists
    (h_top.eventually (eventually_ge_atTop p)).exists

/-- A continuous function which tendsto `Filter.atBot` along `Filter.atTop` and to `Filter.atTop`
along `atBot` is surjective. -/
theorem Continuous.surjective' {f : Œ± ‚Üí Œ¥} (hf : Continuous f) (h_top : Tendsto f atBot atTop)
    (h_bot : Tendsto f atTop atBot) : Function.Surjective f :=
  Continuous.surjective (Œ± := Œ±·µí·µà) hf h_top h_bot

/-- If a function `f : Œ± ‚Üí Œ≤` is continuous on a nonempty interval `s`, its restriction to `s`
tends to `at_bot : Filter Œ≤` along `at_bot : Filter ‚Ü•s` and tends to `Filter.atTop : Filter Œ≤` along
`Filter.atTop : Filter ‚Ü•s`, then the restriction of `f` to `s` is surjective. We formulate the
conclusion as `Function.surjOn f s Set.univ`. -/
theorem ContinuousOn.surjOn_of_tendsto {f : Œ± ‚Üí Œ¥} {s : Set Œ±} [OrdConnected s] (hs : s.Nonempty)
    (hf : ContinuousOn f s) (hbot : Tendsto (fun x : s => f x) atBot atBot)
    (htop : Tendsto (fun x : s => f x) atTop atTop) : SurjOn f s univ :=
  haveI := Classical.inhabited_of_nonempty hs.to_subtype
  surjOn_iff_surjective.2 <| hf.restrict.surjective htop hbot

/-- If a function `f : Œ± ‚Üí Œ≤` is continuous on a nonempty interval `s`, its restriction to `s`
tends to `Filter.atTop : Filter Œ≤` along `Filter.atBot : Filter ‚Ü•s` and tends to
`Filter.atBot : Filter Œ≤` along `Filter.atTop : Filter ‚Ü•s`, then the restriction of `f` to `s` is
surjective. We formulate the conclusion as `Function.surjOn f s Set.univ`. -/
theorem ContinuousOn.surjOn_of_tendsto' {f : Œ± ‚Üí Œ¥} {s : Set Œ±} [OrdConnected s] (hs : s.Nonempty)
    (hf : ContinuousOn f s) (hbot : Tendsto (fun x : s => f x) atBot atTop)
    (htop : Tendsto (fun x : s => f x) atTop atBot) : SurjOn f s univ :=
  ContinuousOn.surjOn_of_tendsto (Œ¥ := Œ¥·µí·µà) hs hf hbot htop

theorem Continuous.strictMono_of_inj_boundedOrder [BoundedOrder Œ±] {f : Œ± ‚Üí Œ¥}
    (hf_c : Continuous f) (hf : f ‚ä• ‚â§ f ‚ä§) (hf_i : Injective f) : StrictMono f := by
  intro a b hab
  by_contra! h
  have H : f b < f a := lt_of_le_of_ne h <| hf_i.ne hab.ne'
  by_cases ha : f a ‚â§ f ‚ä•
  ¬∑ obtain ‚ü®u, hu‚ü© := intermediate_value_Ioc le_top hf_c.continuousOn ‚ü®H.trans_le ha, hf‚ü©
    have : u = ‚ä• := hf_i hu.2
    aesop
  ¬∑ by_cases hb : f ‚ä• < f b
    ¬∑ obtain ‚ü®u, hu‚ü© := intermediate_value_Ioo bot_le hf_c.continuousOn ‚ü®hb, H‚ü©
      rw [hf_i hu.2] at hu
      exact (hab.trans hu.1.2).false
    ¬∑ push_neg at ha hb
      replace hb : f b < f ‚ä• := lt_of_le_of_ne hb <| hf_i.ne (lt_of_lt_of_le' hab bot_le).ne'
      obtain ‚ü®u, hu‚ü© := intermediate_value_Ioo' hab.le hf_c.continuousOn ‚ü®hb, ha‚ü©
      have : u = ‚ä• := hf_i hu.2
      aesop

theorem Continuous.strictAnti_of_inj_boundedOrder [BoundedOrder Œ±] {f : Œ± ‚Üí Œ¥}
    (hf_c : Continuous f) (hf : f ‚ä§ ‚â§ f ‚ä•) (hf_i : Injective f) : StrictAnti f :=
  hf_c.strictMono_of_inj_boundedOrder (Œ¥ := Œ¥·µí·µà) hf hf_i

theorem Continuous.strictMono_of_inj_boundedOrder' [BoundedOrder Œ±] {f : Œ± ‚Üí Œ¥}
    (hf_c : Continuous f) (hf_i : Injective f) : StrictMono f ‚à® StrictAnti f :=
  (le_total (f ‚ä•) (f ‚ä§)).imp
    (hf_c.strictMono_of_inj_boundedOrder ¬∑ hf_i)
    (hf_c.strictAnti_of_inj_boundedOrder ¬∑ hf_i)

/-- Suppose `Œ±` is equipped with a conditionally complete linear dense order and `f : Œ± ‚Üí Œ¥` is
continuous and injective. Then `f` is strictly monotone (increasing) if
it is strictly monotone (increasing) on some closed interval `[a, b]`. -/
theorem Continuous.strictMonoOn_of_inj_rigidity {f : Œ± ‚Üí Œ¥}
    (hf_c : Continuous f) (hf_i : Injective f) {a b : Œ±} (hab : a < b)
    (hf_mono : StrictMonoOn f (Icc a b)) : StrictMono f := by
  intro x y hxy
  let s := min a x
  let t := max b y
  have hsa : s ‚â§ a := min_le_left a x
  have hbt : b ‚â§ t := le_max_left b y
  have hf_mono_st : StrictMonoOn f (Icc s t) ‚à® StrictAntiOn f (Icc s t) := by
    have : Fact (s ‚â§ t) := ‚ü®hsa.trans <| hbt.trans' hab.le‚ü©
    have := Continuous.strictMono_of_inj_boundedOrder' (f := Set.restrict (Icc s t) f)
      hf_c.continuousOn.restrict hf_i.injOn.injective
    exact this.imp strictMono_restrict.mp strictAntiOn_iff_strictAnti.mpr
  have (h : StrictAntiOn f (Icc s t)) : False := by
    have : Icc a b ‚äÜ Icc s t := Icc_subset_Icc hsa hbt
    replace : StrictAntiOn f (Icc a b) := StrictAntiOn.mono h this
    replace : IsAntichain (¬∑ ‚â§ ¬∑) (Icc a b) :=
      IsAntichain.of_strictMonoOn_antitoneOn hf_mono this.antitoneOn
    exact this.not_lt (left_mem_Icc.mpr (le_of_lt hab)) (right_mem_Icc.mpr (le_of_lt hab)) hab
  replace hf_mono_st : StrictMonoOn f (Icc s t) := hf_mono_st.resolve_right this
  have hsx : s ‚â§ x := min_le_right a x
  have hyt : y ‚â§ t := le_max_right b y
  replace : Icc x y ‚äÜ Icc s t := Icc_subset_Icc hsx hyt
  replace : StrictMonoOn f (Icc x y) := StrictMonoOn.mono hf_mono_st this
  exact this (left_mem_Icc.mpr (le_of_lt hxy)) (right_mem_Icc.mpr (le_of_lt hxy)) hxy

/-- Suppose `f : [a, b] ‚Üí Œ¥` is
continuous and injective. Then `f` is strictly monotone (increasing) if `f(a) ‚â§ f(b)`. -/
theorem ContinuousOn.strictMonoOn_of_injOn_Icc {a b : Œ±} {f : Œ± ‚Üí Œ¥}
    (hab : a ‚â§ b) (hfab : f a ‚â§ f b)
    (hf_c : ContinuousOn f (Icc a b)) (hf_i : InjOn f (Icc a b)) :
    StrictMonoOn f (Icc a b) := by
  have : Fact (a ‚â§ b) := ‚ü®hab‚ü©
  refine StrictMono.of_restrict ?_
  set g : Icc a b ‚Üí Œ¥ := Set.restrict (Icc a b) f
  have hgab : g ‚ä• ‚â§ g ‚ä§ := by aesop
  exact Continuous.strictMono_of_inj_boundedOrder (f := g) hf_c.restrict hgab hf_i.injective

/-- Suppose `f : [a, b] ‚Üí Œ¥` is
continuous and injective. Then `f` is strictly antitone (decreasing) if `f(b) ‚â§ f(a)`. -/
theorem ContinuousOn.strictAntiOn_of_injOn_Icc {a b : Œ±} {f : Œ± ‚Üí Œ¥}
    (hab : a ‚â§ b) (hfab : f b ‚â§ f a)
    (hf_c : ContinuousOn f (Icc a b)) (hf_i : InjOn f (Icc a b)) :
    StrictAntiOn f (Icc a b) := ContinuousOn.strictMonoOn_of_injOn_Icc (Œ¥ := Œ¥·µí·µà) hab hfab hf_c hf_i

/-- Suppose `f : [a, b] ‚Üí Œ¥` is continuous and injective. Then `f` is strictly monotone
or antitone (increasing or decreasing). -/
theorem ContinuousOn.strictMonoOn_of_injOn_Icc' {a b : Œ±} {f : Œ± ‚Üí Œ¥} (hab : a ‚â§ b)
    (hf_c : ContinuousOn f (Icc a b)) (hf_i : InjOn f (Icc a b)) :
    StrictMonoOn f (Icc a b) ‚à® StrictAntiOn f (Icc a b) :=
  (le_total (f a) (f b)).imp
    (ContinuousOn.strictMonoOn_of_injOn_Icc hab ¬∑ hf_c hf_i)
    (ContinuousOn.strictAntiOn_of_injOn_Icc hab ¬∑ hf_c hf_i)

/-- Suppose `Œ±` is equipped with a conditionally complete linear dense order and `f : Œ± ‚Üí Œ¥` is
continuous and injective. Then `f` is strictly monotone or antitone (increasing or decreasing). -/
theorem Continuous.strictMono_of_inj {f : Œ± ‚Üí Œ¥}
    (hf_c : Continuous f) (hf_i : Injective f) : StrictMono f ‚à® StrictAnti f := by
  have H {c d : Œ±} (hcd : c < d) : StrictMono f ‚à® StrictAnti f :=
    (hf_c.continuousOn.strictMonoOn_of_injOn_Icc' hcd.le hf_i.injOn).imp
      (hf_c.strictMonoOn_of_inj_rigidity hf_i hcd)
      (hf_c.strictMonoOn_of_inj_rigidity (Œ¥ := Œ¥·µí·µà) hf_i hcd)
  by_cases hn : Nonempty Œ±
  ¬∑ let a : Œ± := Classical.choice ‚Äπ_‚Ä∫
    by_cases h : ‚àÉ b : Œ±, a ‚â† b
    ¬∑ choose b hb using h
      by_cases hab : a < b
      ¬∑ exact H hab
      ¬∑ push_neg at hab
        have : b < a := by exact Ne.lt_of_le (id (Ne.symm hb)) hab
        exact H this
    ¬∑ push_neg at h
      haveI : Subsingleton Œ± := ‚ü®fun c d => Trans.trans (h c).symm (h d)‚ü©
      exact Or.inl <| Subsingleton.strictMono f
  ¬∑ aesop

/-- Every continuous injective `f : (a, b) ‚Üí Œ¥` is strictly monotone
or antitone (increasing or decreasing). -/
theorem ContinuousOn.strictMonoOn_of_injOn_Ioo {a b : Œ±} {f : Œ± ‚Üí Œ¥} (hab : a < b)
    (hf_c : ContinuousOn f (Ioo a b)) (hf_i : InjOn f (Ioo a b)) :
    StrictMonoOn f (Ioo a b) ‚à® StrictAntiOn f (Ioo a b) := by
  haveI : Inhabited (Ioo a b) := Classical.inhabited_of_nonempty (nonempty_Ioo_subtype hab)
  let g : Ioo a b ‚Üí Œ¥ := Set.restrict (Ioo a b) f
  have : StrictMono g ‚à® StrictAnti g :=
    Continuous.strictMono_of_inj hf_c.restrict hf_i.injective
  exact this.imp strictMono_restrict.mp strictAntiOn_iff_strictAnti.mpr
