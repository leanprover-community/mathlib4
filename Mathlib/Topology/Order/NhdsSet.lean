/-
Copyright (c) 2023 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Topology.Order.Basic

/-!
# Set neighborhoods of intervals

In this file we prove basic theorems about `ğ“Ë¢ s`,
where `s` is one of the intervals
`Set.Ici`, `Set.Iic`, `Set.Ioi`, `Set.Iio`, `Set.Ico`, `Set.Ioc`, `Set.Ioo`, and `Set.Icc`.

First, we prove lemmas in terms of filter equalities.
Then we prove lemmas about `s âˆˆ ğ“Ë¢ t`, where both `s` and `t` are intervals.
Finally, we prove a few lemmas about filter bases of `ğ“Ë¢ (Iic a)` and `ğ“Ë¢ (Ici a)`.
-/


open Set Filter OrderDual
open scoped Topology

section OrderClosedTopology

variable {Î± : Type*} [LinearOrder Î±] [TopologicalSpace Î±] [OrderClosedTopology Î±] {a b c d : Î±}

/-!
# Formulae for `ğ“Ë¢` of intervals
-/

@[simp] theorem nhdsSet_Ioi : ğ“Ë¢ (Ioi a) = ğ“Ÿ (Ioi a) := isOpen_Ioi.nhdsSet_eq
@[simp] theorem nhdsSet_Iio : ğ“Ë¢ (Iio a) = ğ“Ÿ (Iio a) := isOpen_Iio.nhdsSet_eq
@[simp] theorem nhdsSet_Ioo : ğ“Ë¢ (Ioo a b) = ğ“Ÿ (Ioo a b) := isOpen_Ioo.nhdsSet_eq

theorem nhdsSet_Ici : ğ“Ë¢ (Ici a) = ğ“ a âŠ” ğ“Ÿ (Ioi a) := by
  rw [â† Ioi_insert, nhdsSet_insert, nhdsSet_Ioi]

theorem nhdsSet_Iic : ğ“Ë¢ (Iic a) = ğ“ a âŠ” ğ“Ÿ (Iio a) := nhdsSet_Ici (Î± := Î±áµ’áµˆ)

theorem nhdsSet_Ico (h : a < b) : ğ“Ë¢ (Ico a b) = ğ“ a âŠ” ğ“Ÿ (Ioo a b) := by
  rw [â† Ioo_insert_left h, nhdsSet_insert, nhdsSet_Ioo]

theorem nhdsSet_Ioc (h : a < b) : ğ“Ë¢ (Ioc a b) = ğ“ b âŠ” ğ“Ÿ (Ioo a b) := by
  rw [â† Ioo_insert_right h, nhdsSet_insert, nhdsSet_Ioo]

theorem nhdsSet_Icc (h : a â‰¤ b) : ğ“Ë¢ (Icc a b) = ğ“ a âŠ” ğ“ b âŠ” ğ“Ÿ (Ioo a b) := by
  rcases h.eq_or_lt with rfl | hlt
  Â· simp
  Â· rw [â† Ioc_insert_left h, nhdsSet_insert, nhdsSet_Ioc hlt, sup_assoc]

/-!
### Lemmas about `Ixi _ âˆˆ ğ“Ë¢ (Set.Ici _)`
-/

@[simp]
theorem Ioi_mem_nhdsSet_Ici_iff : Ioi a âˆˆ ğ“Ë¢ (Ici b) â†” a < b := by
  rw [isOpen_Ioi.mem_nhdsSet, Ici_subset_Ioi]

alias âŸ¨_, Ioi_mem_nhdsSet_IciâŸ© := Ioi_mem_nhdsSet_Ici_iff

theorem Ici_mem_nhdsSet_Ici (h : a < b) : Ici a âˆˆ ğ“Ë¢ (Ici b) :=
  mem_of_superset (Ioi_mem_nhdsSet_Ici h) Ioi_subset_Ici_self

/-!
### Lemmas about `Iix _ âˆˆ ğ“Ë¢ (Set.Iic _)`
-/

theorem Iio_mem_nhdsSet_Iic_iff : Iio b âˆˆ ğ“Ë¢ (Iic a) â†” a < b :=
  Ioi_mem_nhdsSet_Ici_iff (Î± := Î±áµ’áµˆ)

alias âŸ¨_, Iio_mem_nhdsSet_IicâŸ© := Iio_mem_nhdsSet_Iic_iff

theorem Iic_mem_nhdsSet_Iic (h : a < b) : Iic b âˆˆ ğ“Ë¢ (Iic a) :=
  Ici_mem_nhdsSet_Ici (Î± := Î±áµ’áµˆ) h

/-!
### Lemmas about `Ixx _ ?_ âˆˆ ğ“Ë¢ (Set.Icc _ _)`
-/

theorem Ioi_mem_nhdsSet_Icc (h : a < b) : Ioi a âˆˆ ğ“Ë¢ (Icc b c) :=
  nhdsSet_mono Icc_subset_Ici_self <| Ioi_mem_nhdsSet_Ici h

theorem Ici_mem_nhdsSet_Icc (h : a < b) : Ici a âˆˆ ğ“Ë¢ (Icc b c) :=
  mem_of_superset (Ioi_mem_nhdsSet_Icc h) Ioi_subset_Ici_self

theorem Iio_mem_nhdsSet_Icc (h : b < c) : Iio c âˆˆ ğ“Ë¢ (Icc a b) :=
  nhdsSet_mono Icc_subset_Iic_self <| Iio_mem_nhdsSet_Iic h

theorem Iic_mem_nhdsSet_Icc (h : b < c) : Iic c âˆˆ ğ“Ë¢ (Icc a b) :=
  mem_of_superset (Iio_mem_nhdsSet_Icc h) Iio_subset_Iic_self

theorem Ioo_mem_nhdsSet_Icc (h : a < b) (h' : c < d) : Ioo a d âˆˆ ğ“Ë¢ (Icc b c) :=
  inter_mem (Ioi_mem_nhdsSet_Icc h) (Iio_mem_nhdsSet_Icc h')

theorem Ico_mem_nhdsSet_Icc (h : a < b) (h' : c < d) : Ico a d âˆˆ ğ“Ë¢ (Icc b c) :=
  inter_mem (Ici_mem_nhdsSet_Icc h) (Iio_mem_nhdsSet_Icc h')

theorem Ioc_mem_nhdsSet_Icc (h : a < b) (h' : c < d) : Ioc a d âˆˆ ğ“Ë¢ (Icc b c) :=
  inter_mem (Ioi_mem_nhdsSet_Icc h) (Iic_mem_nhdsSet_Icc h')

theorem Icc_mem_nhdsSet_Icc (h : a < b) (h' : c < d) : Icc a d âˆˆ ğ“Ë¢ (Icc b c) :=
  inter_mem (Ici_mem_nhdsSet_Icc h) (Iic_mem_nhdsSet_Icc h')

/-!
### Lemmas about `Ixx _ ?_ âˆˆ ğ“Ë¢ (Set.Ico _ _)`
-/

theorem Ici_mem_nhdsSet_Ico (h : a < b) : Ici a âˆˆ ğ“Ë¢ (Ico b c) :=
  nhdsSet_mono Ico_subset_Icc_self <| Ici_mem_nhdsSet_Icc h

theorem Ioi_mem_nhdsSet_Ico (h : a < b) : Ioi a âˆˆ ğ“Ë¢ (Ico b c) :=
  nhdsSet_mono Ico_subset_Icc_self <| Ioi_mem_nhdsSet_Icc h

theorem Iio_mem_nhdsSet_Ico (h : b â‰¤ c) : Iio c âˆˆ ğ“Ë¢ (Ico a b) :=
  nhdsSet_mono Ico_subset_Iio_self <| by simpa

theorem Iic_mem_nhdsSet_Ico (h : b â‰¤ c) : Iic c âˆˆ ğ“Ë¢ (Ico a b) :=
  mem_of_superset (Iio_mem_nhdsSet_Ico h) Iio_subset_Iic_self

theorem Ioo_mem_nhdsSet_Ico (h : a < b) (h' : c â‰¤ d) : Ioo a d âˆˆ ğ“Ë¢ (Ico b c) :=
  inter_mem (Ioi_mem_nhdsSet_Ico h) (Iio_mem_nhdsSet_Ico h')

theorem Icc_mem_nhdsSet_Ico (h : a < b) (h' : c â‰¤ d) : Icc a d âˆˆ ğ“Ë¢ (Ico b c) :=
  inter_mem (Ici_mem_nhdsSet_Ico h) (Iic_mem_nhdsSet_Ico h')

theorem Ioc_mem_nhdsSet_Ico (h : a < b) (h' : c â‰¤ d) : Ioc a d âˆˆ ğ“Ë¢ (Ico b c) :=
  inter_mem (Ioi_mem_nhdsSet_Ico h) (Iic_mem_nhdsSet_Ico h')

theorem Ico_mem_nhdsSet_Ico (h : a < b) (h' : c â‰¤ d) : Ico a d âˆˆ ğ“Ë¢ (Ico b c) :=
  inter_mem (Ici_mem_nhdsSet_Ico h) (Iio_mem_nhdsSet_Ico h')

/-!
### Lemmas about `Ixx _ ?_ âˆˆ ğ“Ë¢ (Set.Ioc _ _)`
-/

theorem Ioi_mem_nhdsSet_Ioc (h : a â‰¤ b) : Ioi a âˆˆ ğ“Ë¢ (Ioc b c) :=
  nhdsSet_mono Ioc_subset_Ioi_self <| by simpa

theorem Iio_mem_nhdsSet_Ioc (h : b < c) : Iio c âˆˆ ğ“Ë¢ (Ioc a b) :=
  nhdsSet_mono Ioc_subset_Icc_self <| Iio_mem_nhdsSet_Icc h

theorem Ici_mem_nhdsSet_Ioc (h : a â‰¤ b) : Ici a âˆˆ ğ“Ë¢ (Ioc b c) :=
  mem_of_superset (Ioi_mem_nhdsSet_Ioc h) Ioi_subset_Ici_self

theorem Iic_mem_nhdsSet_Ioc (h : b < c) : Iic c âˆˆ ğ“Ë¢ (Ioc a b) :=
  nhdsSet_mono Ioc_subset_Icc_self <| Iic_mem_nhdsSet_Icc h

theorem Ioo_mem_nhdsSet_Ioc (h : a â‰¤ b) (h' : c < d) : Ioo a d âˆˆ ğ“Ë¢ (Ioc b c) :=
  inter_mem (Ioi_mem_nhdsSet_Ioc h) (Iio_mem_nhdsSet_Ioc h')

theorem Icc_mem_nhdsSet_Ioc (h : a â‰¤ b) (h' : c < d) : Icc a d âˆˆ ğ“Ë¢ (Ioc b c) :=
  inter_mem (Ici_mem_nhdsSet_Ioc h) (Iic_mem_nhdsSet_Ioc h')

theorem Ioc_mem_nhdsSet_Ioc (h : a â‰¤ b) (h' : c < d) : Ioc a d âˆˆ ğ“Ë¢ (Ioc b c) :=
  inter_mem (Ioi_mem_nhdsSet_Ioc h) (Iic_mem_nhdsSet_Ioc h')

theorem Ico_mem_nhdsSet_Ioc (h : a â‰¤ b) (h' : c < d) : Ico a d âˆˆ ğ“Ë¢ (Ioc b c) :=
  inter_mem (Ici_mem_nhdsSet_Ioc h) (Iio_mem_nhdsSet_Ioc h')

end OrderClosedTopology

/-!
### Filter bases of `ğ“Ë¢ (Iic a)` and `ğ“Ë¢ (Ici a)`
-/

variable {Î± : Type*} [LinearOrder Î±] [TopologicalSpace Î±] [OrderTopology Î±]

theorem hasBasis_nhdsSet_Iic_Iio (a : Î±) [h : Nonempty (Ioi a)] :
    HasBasis (ğ“Ë¢ (Iic a)) (a < Â·) Iio := by
  refine âŸ¨fun s â†¦ âŸ¨fun hs â†¦ ?_, fun âŸ¨b, hab, hbâŸ© â†¦ mem_of_superset (Iio_mem_nhdsSet_Iic hab) hbâŸ©âŸ©
  rw [nhdsSet_Iic, mem_sup, mem_principal] at hs
  rcases exists_Ico_subset_of_mem_nhds hs.1 (Set.nonempty_coe_sort.1 h) with âŸ¨b, hab, hbsâŸ©
  exact âŸ¨b, hab, Iio_subset_Iio_union_Ico.trans (union_subset hs.2 hbs)âŸ©

theorem hasBasis_nhdsSet_Iic_Iic (a : Î±) [NeBot (ğ“[>] a)] :
    HasBasis (ğ“Ë¢ (Iic a)) (a < Â·) Iic := by
  have : Nonempty (Ioi a) :=
    (Filter.nonempty_of_mem (self_mem_nhdsWithin : Ioi a âˆˆ ğ“[>] a)).to_subtype
  refine (hasBasis_nhdsSet_Iic_Iio _).to_hasBasis
    (fun c hc â†¦ ?_) (fun _ h â†¦ âŸ¨_, h, Iio_subset_Iic_selfâŸ©)
  simpa only [Iic_subset_Iio] using (Filter.nonempty_of_mem <| Ioo_mem_nhdsWithin_Ioi' hc)

@[simp]
theorem Iic_mem_nhdsSet_Iic_iff {a b : Î±} [NeBot (ğ“[>] b)] : Iic a âˆˆ ğ“Ë¢ (Iic b) â†” b < a :=
  (hasBasis_nhdsSet_Iic_Iic b).mem_iff.trans
    âŸ¨fun âŸ¨_c, hbc, hcaâŸ© â†¦ hbc.trans_le (Iic_subset_Iic.1 hca), fun h â†¦ âŸ¨_, h, Subset.rflâŸ©âŸ©

theorem hasBasis_nhdsSet_Ici_Ioi (a : Î±) [Nonempty (Iio a)] :
    HasBasis (ğ“Ë¢ (Ici a)) (Â· < a) Ioi :=
  have : Nonempty (Ioi (toDual a)) := â€¹_â€º; hasBasis_nhdsSet_Iic_Iio (toDual a)

theorem hasBasis_nhdsSet_Ici_Ici (a : Î±) [NeBot (ğ“[<] a)] :
    HasBasis (ğ“Ë¢ (Ici a)) (Â· < a) Ici :=
  have : NeBot (ğ“[>] (toDual a)) := â€¹_â€º; hasBasis_nhdsSet_Iic_Iic (toDual a)

@[simp]
theorem Ici_mem_nhdsSet_Ici_iff {a b : Î±} [NeBot (ğ“[<] b)] : Ici a âˆˆ ğ“Ë¢ (Ici b) â†” a < b :=
  have : NeBot (ğ“[>] (toDual b)) := â€¹_â€º; Iic_mem_nhdsSet_Iic_iff (a := toDual a) (b := toDual b)
