/-
Copyright (c) 2021 Christopher Hoskin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christopher Hoskin
-/
import Mathlib.Topology.Constructions
import Mathlib.Topology.Order.OrderClosed

/-!
# Topological lattices

In this file we define mixin classes `ContinuousInf` and `ContinuousSup`. We define the
class `TopologicalLattice` as a topological space and lattice `L` extending `ContinuousInf` and
`ContinuousSup`.

## References

* [Gierz et al, A Compendium of Continuous Lattices][GierzEtAl1980]

## Tags

topological, lattice
-/

open Filter

open Topology

/-- Let `L` be a topological space and let `LÃ—L` be equipped with the product topology and let
`âŠ“:LÃ—L â†’ L` be an infimum. Then `L` is said to have *(jointly) continuous infimum* if the map
`âŠ“:LÃ—L â†’ L` is continuous.
-/
class ContinuousInf (L : Type*) [TopologicalSpace L] [Min L] : Prop where
  /-- The infimum is continuous -/
  continuous_inf : Continuous fun p : L Ã— L => p.1 âŠ“ p.2

/-- Let `L` be a topological space and let `LÃ—L` be equipped with the product topology and let
`âŠ“:LÃ—L â†’ L` be a supremum. Then `L` is said to have *(jointly) continuous supremum* if the map
`âŠ“:LÃ—L â†’ L` is continuous.
-/
class ContinuousSup (L : Type*) [TopologicalSpace L] [Max L] : Prop where
  /-- The supremum is continuous -/
  continuous_sup : Continuous fun p : L Ã— L => p.1 âŠ” p.2

-- see Note [lower instance priority]
instance (priority := 100) OrderDual.continuousSup (L : Type*) [TopologicalSpace L] [Min L]
    [ContinuousInf L] : ContinuousSup Láµ’áµˆ where
  continuous_sup := @ContinuousInf.continuous_inf L _ _ _

-- see Note [lower instance priority]
instance (priority := 100) OrderDual.continuousInf (L : Type*) [TopologicalSpace L] [Max L]
    [ContinuousSup L] : ContinuousInf Láµ’áµˆ where
  continuous_inf := @ContinuousSup.continuous_sup L _ _ _

/-- Let `L` be a lattice equipped with a topology such that `L` has continuous infimum and supremum.
Then `L` is said to be a *topological lattice*.
-/
class TopologicalLattice (L : Type*) [TopologicalSpace L] [Lattice L] : Prop
  extends ContinuousInf L, ContinuousSup L

-- see Note [lower instance priority]
instance (priority := 100) OrderDual.topologicalLattice (L : Type*) [TopologicalSpace L]
    [Lattice L] [TopologicalLattice L] : TopologicalLattice Láµ’áµˆ where

-- see Note [lower instance priority]
instance (priority := 100) LinearOrder.topologicalLattice {L : Type*} [TopologicalSpace L]
    [LinearOrder L] [OrderClosedTopology L] : TopologicalLattice L where
  continuous_inf := continuous_min
  continuous_sup := continuous_max

variable {L X : Type*} [TopologicalSpace L] [TopologicalSpace X]

@[continuity]
theorem continuous_inf [Min L] [ContinuousInf L] : Continuous fun p : L Ã— L => p.1 âŠ“ p.2 :=
  ContinuousInf.continuous_inf

@[continuity, fun_prop]
theorem Continuous.inf [Min L] [ContinuousInf L] {f g : X â†’ L} (hf : Continuous f)
    (hg : Continuous g) : Continuous fun x => f x âŠ“ g x :=
  continuous_inf.comp (hf.prodMk hg :)

@[continuity]
theorem continuous_sup [Max L] [ContinuousSup L] : Continuous fun p : L Ã— L => p.1 âŠ” p.2 :=
  ContinuousSup.continuous_sup

@[continuity, fun_prop]
theorem Continuous.sup [Max L] [ContinuousSup L] {f g : X â†’ L} (hf : Continuous f)
    (hg : Continuous g) : Continuous fun x => f x âŠ” g x :=
  continuous_sup.comp (hf.prodMk hg :)

namespace Filter.Tendsto

section SupInf

variable {Î± : Type*} {l : Filter Î±} {f g : Î± â†’ L} {x y : L}

lemma sup_nhds' [Max L] [ContinuousSup L] (hf : Tendsto f l (ğ“ x)) (hg : Tendsto g l (ğ“ y)) :
    Tendsto (f âŠ” g) l (ğ“ (x âŠ” y)) :=
  (continuous_sup.tendsto _).comp (hf.prodMk_nhds hg)

lemma sup_nhds [Max L] [ContinuousSup L] (hf : Tendsto f l (ğ“ x)) (hg : Tendsto g l (ğ“ y)) :
    Tendsto (fun i => f i âŠ” g i) l (ğ“ (x âŠ” y)) :=
  hf.sup_nhds' hg

lemma inf_nhds' [Min L] [ContinuousInf L] (hf : Tendsto f l (ğ“ x)) (hg : Tendsto g l (ğ“ y)) :
    Tendsto (f âŠ“ g) l (ğ“ (x âŠ“ y)) :=
  (continuous_inf.tendsto _).comp (hf.prodMk_nhds hg)

lemma inf_nhds [Min L] [ContinuousInf L] (hf : Tendsto f l (ğ“ x)) (hg : Tendsto g l (ğ“ y)) :
    Tendsto (fun i => f i âŠ“ g i) l (ğ“ (x âŠ“ y)) :=
  hf.inf_nhds' hg

end SupInf

open Finset

variable {Î¹ Î± : Type*} {s : Finset Î¹} {f : Î¹ â†’ Î± â†’ L} {l : Filter Î±} {g : Î¹ â†’ L}

lemma finset_sup'_nhds [SemilatticeSup L] [ContinuousSup L]
    (hne : s.Nonempty) (hs : âˆ€ i âˆˆ s, Tendsto (f i) l (ğ“ (g i))) :
    Tendsto (s.sup' hne f) l (ğ“ (s.sup' hne g)) := by
  induction hne using Finset.Nonempty.cons_induction with
  | singleton => simpa using hs
  | cons a s ha hne ihs =>
    rw [forall_mem_cons] at hs
    simp only [sup'_cons, hne]
    exact hs.1.sup_nhds (ihs hs.2)

lemma finset_sup'_nhds_apply [SemilatticeSup L] [ContinuousSup L]
    (hne : s.Nonempty) (hs : âˆ€ i âˆˆ s, Tendsto (f i) l (ğ“ (g i))) :
    Tendsto (fun a â†¦ s.sup' hne (f Â· a)) l (ğ“ (s.sup' hne g)) := by
  simpa only [â† Finset.sup'_apply] using finset_sup'_nhds hne hs

lemma finset_inf'_nhds [SemilatticeInf L] [ContinuousInf L]
    (hne : s.Nonempty) (hs : âˆ€ i âˆˆ s, Tendsto (f i) l (ğ“ (g i))) :
    Tendsto (s.inf' hne f) l (ğ“ (s.inf' hne g)) :=
  finset_sup'_nhds (L := Láµ’áµˆ) hne hs

lemma finset_inf'_nhds_apply [SemilatticeInf L] [ContinuousInf L]
    (hne : s.Nonempty) (hs : âˆ€ i âˆˆ s, Tendsto (f i) l (ğ“ (g i))) :
    Tendsto (fun a â†¦ s.inf' hne (f Â· a)) l (ğ“ (s.inf' hne g)) :=
  finset_sup'_nhds_apply (L := Láµ’áµˆ) hne hs

lemma finset_sup_nhds [SemilatticeSup L] [OrderBot L] [ContinuousSup L]
    (hs : âˆ€ i âˆˆ s, Tendsto (f i) l (ğ“ (g i))) : Tendsto (s.sup f) l (ğ“ (s.sup g)) := by
  rcases s.eq_empty_or_nonempty with rfl | hne
  Â· simpa using tendsto_const_nhds
  Â· simp only [â† sup'_eq_sup hne]
    exact finset_sup'_nhds hne hs

lemma finset_sup_nhds_apply [SemilatticeSup L] [OrderBot L] [ContinuousSup L]
    (hs : âˆ€ i âˆˆ s, Tendsto (f i) l (ğ“ (g i))) :
    Tendsto (fun a â†¦ s.sup (f Â· a)) l (ğ“ (s.sup g)) := by
  simpa only [â† Finset.sup_apply] using finset_sup_nhds hs

lemma finset_inf_nhds [SemilatticeInf L] [OrderTop L] [ContinuousInf L]
    (hs : âˆ€ i âˆˆ s, Tendsto (f i) l (ğ“ (g i))) : Tendsto (s.inf f) l (ğ“ (s.inf g)) :=
  finset_sup_nhds (L := Láµ’áµˆ) hs

lemma finset_inf_nhds_apply [SemilatticeInf L] [OrderTop L] [ContinuousInf L]
    (hs : âˆ€ i âˆˆ s, Tendsto (f i) l (ğ“ (g i))) :
    Tendsto (fun a â†¦ s.inf (f Â· a)) l (ğ“ (s.inf g)) :=
  finset_sup_nhds_apply (L := Láµ’áµˆ) hs

end Filter.Tendsto

section Sup

variable [Max L] [ContinuousSup L] {f g : X â†’ L} {s : Set X} {x : X}

lemma ContinuousAt.sup' (hf : ContinuousAt f x) (hg : ContinuousAt g x) :
    ContinuousAt (f âŠ” g) x :=
  hf.sup_nhds' hg

@[fun_prop]
lemma ContinuousAt.sup (hf : ContinuousAt f x) (hg : ContinuousAt g x) :
    ContinuousAt (fun a â†¦ f a âŠ” g a) x :=
  hf.sup' hg

lemma ContinuousWithinAt.sup' (hf : ContinuousWithinAt f s x) (hg : ContinuousWithinAt g s x) :
    ContinuousWithinAt (f âŠ” g) s x :=
  hf.sup_nhds' hg

lemma ContinuousWithinAt.sup (hf : ContinuousWithinAt f s x) (hg : ContinuousWithinAt g s x) :
    ContinuousWithinAt (fun a â†¦ f a âŠ” g a) s x :=
  hf.sup' hg

lemma ContinuousOn.sup' (hf : ContinuousOn f s) (hg : ContinuousOn g s) :
    ContinuousOn (f âŠ” g) s := fun x hx â†¦
  (hf x hx).sup' (hg x hx)

@[fun_prop]
lemma ContinuousOn.sup (hf : ContinuousOn f s) (hg : ContinuousOn g s) :
    ContinuousOn (fun a â†¦ f a âŠ” g a) s :=
  hf.sup' hg

lemma Continuous.sup' (hf : Continuous f) (hg : Continuous g) : Continuous (f âŠ” g) := hf.sup hg

end Sup

section Inf

variable [Min L] [ContinuousInf L] {f g : X â†’ L} {s : Set X} {x : X}

lemma ContinuousAt.inf' (hf : ContinuousAt f x) (hg : ContinuousAt g x) :
    ContinuousAt (f âŠ“ g) x :=
  hf.inf_nhds' hg

@[fun_prop]
lemma ContinuousAt.inf (hf : ContinuousAt f x) (hg : ContinuousAt g x) :
    ContinuousAt (fun a â†¦ f a âŠ“ g a) x :=
  hf.inf' hg

lemma ContinuousWithinAt.inf' (hf : ContinuousWithinAt f s x) (hg : ContinuousWithinAt g s x) :
    ContinuousWithinAt (f âŠ“ g) s x :=
  hf.inf_nhds' hg

lemma ContinuousWithinAt.inf (hf : ContinuousWithinAt f s x) (hg : ContinuousWithinAt g s x) :
    ContinuousWithinAt (fun a â†¦ f a âŠ“ g a) s x :=
  hf.inf' hg

lemma ContinuousOn.inf' (hf : ContinuousOn f s) (hg : ContinuousOn g s) :
    ContinuousOn (f âŠ“ g) s := fun x hx â†¦
  (hf x hx).inf' (hg x hx)

@[fun_prop]
lemma ContinuousOn.inf (hf : ContinuousOn f s) (hg : ContinuousOn g s) :
    ContinuousOn (fun a â†¦ f a âŠ“ g a) s :=
  hf.inf' hg

lemma Continuous.inf' (hf : Continuous f) (hg : Continuous g) : Continuous (f âŠ“ g) := hf.inf hg

end Inf

section FinsetSup'

variable {Î¹ : Type*} [SemilatticeSup L] [ContinuousSup L] {s : Finset Î¹}
  {f : Î¹ â†’ X â†’ L} {t : Set X} {x : X}

lemma ContinuousAt.finset_sup'_apply (hne : s.Nonempty) (hs : âˆ€ i âˆˆ s, ContinuousAt (f i) x) :
    ContinuousAt (fun a â†¦ s.sup' hne (f Â· a)) x :=
  Tendsto.finset_sup'_nhds_apply hne hs

lemma ContinuousAt.finset_sup' (hne : s.Nonempty) (hs : âˆ€ i âˆˆ s, ContinuousAt (f i) x) :
    ContinuousAt (s.sup' hne f) x := by
  simpa only [â† Finset.sup'_apply] using finset_sup'_apply hne hs

lemma ContinuousWithinAt.finset_sup'_apply (hne : s.Nonempty)
    (hs : âˆ€ i âˆˆ s, ContinuousWithinAt (f i) t x) :
    ContinuousWithinAt (fun a â†¦ s.sup' hne (f Â· a)) t x :=
  Tendsto.finset_sup'_nhds_apply hne hs

lemma ContinuousWithinAt.finset_sup' (hne : s.Nonempty)
    (hs : âˆ€ i âˆˆ s, ContinuousWithinAt (f i) t x) : ContinuousWithinAt (s.sup' hne f) t x := by
  simpa only [â† Finset.sup'_apply] using finset_sup'_apply hne hs

lemma ContinuousOn.finset_sup'_apply (hne : s.Nonempty) (hs : âˆ€ i âˆˆ s, ContinuousOn (f i) t) :
    ContinuousOn (fun a â†¦ s.sup' hne (f Â· a)) t := fun x hx â†¦
  ContinuousWithinAt.finset_sup'_apply hne fun i hi â†¦ hs i hi x hx

lemma ContinuousOn.finset_sup' (hne : s.Nonempty) (hs : âˆ€ i âˆˆ s, ContinuousOn (f i) t) :
    ContinuousOn (s.sup' hne f) t := fun x hx â†¦
  ContinuousWithinAt.finset_sup' hne fun i hi â†¦ hs i hi x hx

lemma Continuous.finset_sup'_apply (hne : s.Nonempty) (hs : âˆ€ i âˆˆ s, Continuous (f i)) :
    Continuous (fun a â†¦ s.sup' hne (f Â· a)) :=
  continuous_iff_continuousAt.2 fun _ â†¦ ContinuousAt.finset_sup'_apply _ fun i hi â†¦
    (hs i hi).continuousAt

lemma Continuous.finset_sup' (hne : s.Nonempty) (hs : âˆ€ i âˆˆ s, Continuous (f i)) :
    Continuous (s.sup' hne f) :=
  continuous_iff_continuousAt.2 fun _ â†¦ ContinuousAt.finset_sup' _ fun i hi â†¦ (hs i hi).continuousAt

end FinsetSup'

section FinsetSup

variable {Î¹ : Type*} [SemilatticeSup L] [OrderBot L] [ContinuousSup L] {s : Finset Î¹}
  {f : Î¹ â†’ X â†’ L} {t : Set X} {x : X}

lemma ContinuousAt.finset_sup_apply (hs : âˆ€ i âˆˆ s, ContinuousAt (f i) x) :
    ContinuousAt (fun a â†¦ s.sup (f Â· a)) x :=
  Tendsto.finset_sup_nhds_apply hs

lemma ContinuousAt.finset_sup (hs : âˆ€ i âˆˆ s, ContinuousAt (f i) x) :
    ContinuousAt (s.sup f) x := by
  simpa only [â† Finset.sup_apply] using finset_sup_apply hs

lemma ContinuousWithinAt.finset_sup_apply
    (hs : âˆ€ i âˆˆ s, ContinuousWithinAt (f i) t x) :
    ContinuousWithinAt (fun a â†¦ s.sup (f Â· a)) t x :=
  Tendsto.finset_sup_nhds_apply hs

lemma ContinuousWithinAt.finset_sup
    (hs : âˆ€ i âˆˆ s, ContinuousWithinAt (f i) t x) : ContinuousWithinAt (s.sup f) t x := by
  simpa only [â† Finset.sup_apply] using finset_sup_apply hs

lemma ContinuousOn.finset_sup_apply (hs : âˆ€ i âˆˆ s, ContinuousOn (f i) t) :
    ContinuousOn (fun a â†¦ s.sup (f Â· a)) t := fun x hx â†¦
  ContinuousWithinAt.finset_sup_apply fun i hi â†¦ hs i hi x hx

lemma ContinuousOn.finset_sup (hs : âˆ€ i âˆˆ s, ContinuousOn (f i) t) :
    ContinuousOn (s.sup f) t := fun x hx â†¦
  ContinuousWithinAt.finset_sup fun i hi â†¦ hs i hi x hx

lemma Continuous.finset_sup_apply (hs : âˆ€ i âˆˆ s, Continuous (f i)) :
    Continuous (fun a â†¦ s.sup (f Â· a)) :=
  continuous_iff_continuousAt.2 fun _ â†¦ ContinuousAt.finset_sup_apply fun i hi â†¦
    (hs i hi).continuousAt

lemma Continuous.finset_sup (hs : âˆ€ i âˆˆ s, Continuous (f i)) : Continuous (s.sup f) :=
  continuous_iff_continuousAt.2 fun _ â†¦ ContinuousAt.finset_sup fun i hi â†¦ (hs i hi).continuousAt

end FinsetSup

section FinsetInf'

variable {Î¹ : Type*} [SemilatticeInf L] [ContinuousInf L] {s : Finset Î¹}
  {f : Î¹ â†’ X â†’ L} {t : Set X} {x : X}

lemma ContinuousAt.finset_inf'_apply (hne : s.Nonempty) (hs : âˆ€ i âˆˆ s, ContinuousAt (f i) x) :
    ContinuousAt (fun a â†¦ s.inf' hne (f Â· a)) x :=
  Tendsto.finset_inf'_nhds_apply hne hs

lemma ContinuousAt.finset_inf' (hne : s.Nonempty) (hs : âˆ€ i âˆˆ s, ContinuousAt (f i) x) :
    ContinuousAt (s.inf' hne f) x := by
  simpa only [â† Finset.inf'_apply] using finset_inf'_apply hne hs

lemma ContinuousWithinAt.finset_inf'_apply (hne : s.Nonempty)
    (hs : âˆ€ i âˆˆ s, ContinuousWithinAt (f i) t x) :
    ContinuousWithinAt (fun a â†¦ s.inf' hne (f Â· a)) t x :=
  Tendsto.finset_inf'_nhds_apply hne hs

lemma ContinuousWithinAt.finset_inf' (hne : s.Nonempty)
    (hs : âˆ€ i âˆˆ s, ContinuousWithinAt (f i) t x) : ContinuousWithinAt (s.inf' hne f) t x := by
  simpa only [â† Finset.inf'_apply] using finset_inf'_apply hne hs

lemma ContinuousOn.finset_inf'_apply (hne : s.Nonempty) (hs : âˆ€ i âˆˆ s, ContinuousOn (f i) t) :
    ContinuousOn (fun a â†¦ s.inf' hne (f Â· a)) t := fun x hx â†¦
  ContinuousWithinAt.finset_inf'_apply hne fun i hi â†¦ hs i hi x hx

lemma ContinuousOn.finset_inf' (hne : s.Nonempty) (hs : âˆ€ i âˆˆ s, ContinuousOn (f i) t) :
    ContinuousOn (s.inf' hne f) t := fun x hx â†¦
  ContinuousWithinAt.finset_inf' hne fun i hi â†¦ hs i hi x hx

lemma Continuous.finset_inf'_apply (hne : s.Nonempty) (hs : âˆ€ i âˆˆ s, Continuous (f i)) :
    Continuous (fun a â†¦ s.inf' hne (f Â· a)) :=
  continuous_iff_continuousAt.2 fun _ â†¦ ContinuousAt.finset_inf'_apply _ fun i hi â†¦
    (hs i hi).continuousAt

lemma Continuous.finset_inf' (hne : s.Nonempty) (hs : âˆ€ i âˆˆ s, Continuous (f i)) :
    Continuous (s.inf' hne f) :=
  continuous_iff_continuousAt.2 fun _ â†¦ ContinuousAt.finset_inf' _ fun i hi â†¦ (hs i hi).continuousAt

end FinsetInf'

section FinsetInf

variable {Î¹ : Type*} [SemilatticeInf L] [OrderTop L] [ContinuousInf L] {s : Finset Î¹}
  {f : Î¹ â†’ X â†’ L} {t : Set X} {x : X}

lemma ContinuousAt.finset_inf_apply (hs : âˆ€ i âˆˆ s, ContinuousAt (f i) x) :
    ContinuousAt (fun a â†¦ s.inf (f Â· a)) x :=
  Tendsto.finset_inf_nhds_apply hs

lemma ContinuousAt.finset_inf (hs : âˆ€ i âˆˆ s, ContinuousAt (f i) x) :
    ContinuousAt (s.inf f) x := by
  simpa only [â† Finset.inf_apply] using finset_inf_apply hs

lemma ContinuousWithinAt.finset_inf_apply
    (hs : âˆ€ i âˆˆ s, ContinuousWithinAt (f i) t x) :
    ContinuousWithinAt (fun a â†¦ s.inf (f Â· a)) t x :=
  Tendsto.finset_inf_nhds_apply hs

lemma ContinuousWithinAt.finset_inf
    (hs : âˆ€ i âˆˆ s, ContinuousWithinAt (f i) t x) : ContinuousWithinAt (s.inf f) t x := by
  simpa only [â† Finset.inf_apply] using finset_inf_apply hs

lemma ContinuousOn.finset_inf_apply (hs : âˆ€ i âˆˆ s, ContinuousOn (f i) t) :
    ContinuousOn (fun a â†¦ s.inf (f Â· a)) t := fun x hx â†¦
  ContinuousWithinAt.finset_inf_apply fun i hi â†¦ hs i hi x hx

lemma ContinuousOn.finset_inf (hs : âˆ€ i âˆˆ s, ContinuousOn (f i) t) :
    ContinuousOn (s.inf f) t := fun x hx â†¦
  ContinuousWithinAt.finset_inf fun i hi â†¦ hs i hi x hx

lemma Continuous.finset_inf_apply (hs : âˆ€ i âˆˆ s, Continuous (f i)) :
    Continuous (fun a â†¦ s.inf (f Â· a)) :=
  continuous_iff_continuousAt.2 fun _ â†¦ ContinuousAt.finset_inf_apply fun i hi â†¦
    (hs i hi).continuousAt

lemma Continuous.finset_inf (hs : âˆ€ i âˆˆ s, Continuous (f i)) : Continuous (s.inf f) :=
  continuous_iff_continuousAt.2 fun _ â†¦ ContinuousAt.finset_inf fun i hi â†¦ (hs i hi).continuousAt

end FinsetInf
