/-
Copyright (c) 2021 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import Mathlib.Topology.ContinuousOn

#align_import topology.algebra.order.left_right from "leanprover-community/mathlib"@"bcfa726826abd57587355b4b5b7e78ad6527b7e4"

/-!
# Left and right continuity

In this file we prove a few lemmas about left and right continuous functions:

* `continuousWithinAt_Ioi_iff_Ici`: two definitions of right continuity
  (with `(a, âˆ)` and with `[a, âˆ)`) are equivalent;
* `continuousWithinAt_Iio_iff_Iic`: two definitions of left continuity
  (with `(-âˆ, a)` and with `(-âˆ, a]`) are equivalent;
* `continuousAt_iff_continuous_left_right`, `continuousAt_iff_continuous_left'_right'` :
  a function is continuous at `a` if and only if it is left and right continuous at `a`.

## Tags

left continuous, right continuous
-/


open Set Filter Topology

section Preorder

variable {Î± : Type*} [TopologicalSpace Î±] [Preorder Î±]

lemma frequently_lt_nhds (a : Î±) [NeBot (ğ“[<] a)] : âˆƒá¶  x in ğ“ a, x < a :=
  frequently_iff_neBot.2 â€¹_â€º

lemma frequently_gt_nhds (a : Î±) [NeBot (ğ“[>] a)] : âˆƒá¶  x in ğ“ a, a < x :=
  frequently_iff_neBot.2 â€¹_â€º

theorem Filter.Eventually.exists_lt {a : Î±} [NeBot (ğ“[<] a)] {p : Î± â†’ Prop}
    (h : âˆ€á¶  x in ğ“ a, p x) : âˆƒ b < a, p b :=
  ((frequently_lt_nhds a).and_eventually h).exists
#align filter.eventually.exists_lt Filter.Eventually.exists_lt

theorem Filter.Eventually.exists_gt {a : Î±} [NeBot (ğ“[>] a)] {p : Î± â†’ Prop}
    (h : âˆ€á¶  x in ğ“ a, p x) : âˆƒ b > a, p b :=
  ((frequently_gt_nhds a).and_eventually h).exists
#align filter.eventually.exists_gt Filter.Eventually.exists_gt

theorem nhdsWithin_Ici_neBot {a b : Î±} (Hâ‚‚ : a â‰¤ b) : NeBot (ğ“[Ici a] b) :=
  nhdsWithin_neBot_of_mem Hâ‚‚
#align nhds_within_Ici_ne_bot nhdsWithin_Ici_neBot

instance nhdsWithin_Ici_self_neBot (a : Î±) : NeBot (ğ“[â‰¥] a) :=
  nhdsWithin_Ici_neBot (le_refl a)
#align nhds_within_Ici_self_ne_bot nhdsWithin_Ici_self_neBot

theorem nhdsWithin_Iic_neBot {a b : Î±} (H : a â‰¤ b) : NeBot (ğ“[Iic b] a) :=
  nhdsWithin_neBot_of_mem H
#align nhds_within_Iic_ne_bot nhdsWithin_Iic_neBot

instance nhdsWithin_Iic_self_neBot (a : Î±) : NeBot (ğ“[â‰¤] a) :=
  nhdsWithin_Iic_neBot (le_refl a)
#align nhds_within_Iic_self_ne_bot nhdsWithin_Iic_self_neBot

theorem nhds_left'_le_nhds_ne (a : Î±) : ğ“[<] a â‰¤ ğ“[â‰ ] a :=
  nhdsWithin_mono a fun _ => ne_of_lt
#align nhds_left'_le_nhds_ne nhds_left'_le_nhds_ne

theorem nhds_right'_le_nhds_ne (a : Î±) : ğ“[>] a â‰¤ ğ“[â‰ ] a :=
  nhdsWithin_mono a fun _ => ne_of_gt
#align nhds_right'_le_nhds_ne nhds_right'_le_nhds_ne

-- TODO: add instances for `NeBot (ğ“[<] x)` on (indexed) product types

lemma IsAntichain.interior_eq_empty [âˆ€ x : Î±, (ğ“[<] x).NeBot] {s : Set Î±}
    (hs : IsAntichain (Â· â‰¤ Â·) s) : interior s = âˆ… := by
  refine eq_empty_of_forall_not_mem fun x hx â†¦ ?_
  have : âˆ€á¶  y in ğ“ x, y âˆˆ s := mem_interior_iff_mem_nhds.1 hx
  rcases this.exists_lt with âŸ¨y, hyx, hysâŸ©
  exact hs hys (interior_subset hx) hyx.ne hyx.le
#align is_antichain.interior_eq_empty IsAntichain.interior_eq_empty

lemma IsAntichain.interior_eq_empty' [âˆ€ x : Î±, (ğ“[>] x).NeBot] {s : Set Î±}
    (hs : IsAntichain (Â· â‰¤ Â·) s) : interior s = âˆ… :=
  have : âˆ€ x : Î±áµ’áµˆ, NeBot (ğ“[<] x) := â€¹_â€º
  hs.to_dual.interior_eq_empty

end Preorder

section PartialOrder

variable {Î± Î² : Type*} [TopologicalSpace Î±] [PartialOrder Î±] [TopologicalSpace Î²]

theorem continuousWithinAt_Ioi_iff_Ici {a : Î±} {f : Î± â†’ Î²} :
    ContinuousWithinAt f (Ioi a) a â†” ContinuousWithinAt f (Ici a) a := by
  simp only [â† Ici_diff_left, continuousWithinAt_diff_self]
#align continuous_within_at_Ioi_iff_Ici continuousWithinAt_Ioi_iff_Ici

theorem continuousWithinAt_Iio_iff_Iic {a : Î±} {f : Î± â†’ Î²} :
    ContinuousWithinAt f (Iio a) a â†” ContinuousWithinAt f (Iic a) a :=
  @continuousWithinAt_Ioi_iff_Ici Î±áµ’áµˆ _ _ _ _ _ f
#align continuous_within_at_Iio_iff_Iic continuousWithinAt_Iio_iff_Iic

end PartialOrder

section TopologicalSpace

variable {Î± Î² : Type*} [TopologicalSpace Î±] [LinearOrder Î±] [TopologicalSpace Î²]

theorem nhds_left_sup_nhds_right (a : Î±) : ğ“[â‰¤] a âŠ” ğ“[â‰¥] a = ğ“ a := by
  rw [â† nhdsWithin_union, Iic_union_Ici, nhdsWithin_univ]
#align nhds_left_sup_nhds_right nhds_left_sup_nhds_right

theorem nhds_left'_sup_nhds_right (a : Î±) : ğ“[<] a âŠ” ğ“[â‰¥] a = ğ“ a := by
  rw [â† nhdsWithin_union, Iio_union_Ici, nhdsWithin_univ]
#align nhds_left'_sup_nhds_right nhds_left'_sup_nhds_right

theorem nhds_left_sup_nhds_right' (a : Î±) : ğ“[â‰¤] a âŠ” ğ“[>] a = ğ“ a := by
  rw [â† nhdsWithin_union, Iic_union_Ioi, nhdsWithin_univ]
#align nhds_left_sup_nhds_right' nhds_left_sup_nhds_right'

theorem nhds_left'_sup_nhds_right' (a : Î±) : ğ“[<] a âŠ” ğ“[>] a = ğ“[â‰ ] a := by
  rw [â† nhdsWithin_union, Iio_union_Ioi]
#align nhds_left'_sup_nhds_right' nhds_left'_sup_nhds_right'

theorem continuousAt_iff_continuous_left_right {a : Î±} {f : Î± â†’ Î²} :
    ContinuousAt f a â†” ContinuousWithinAt f (Iic a) a âˆ§ ContinuousWithinAt f (Ici a) a := by
  simp only [ContinuousWithinAt, ContinuousAt, â† tendsto_sup, nhds_left_sup_nhds_right]
#align continuous_at_iff_continuous_left_right continuousAt_iff_continuous_left_right

theorem continuousAt_iff_continuous_left'_right' {a : Î±} {f : Î± â†’ Î²} :
    ContinuousAt f a â†” ContinuousWithinAt f (Iio a) a âˆ§ ContinuousWithinAt f (Ioi a) a := by
  rw [continuousWithinAt_Ioi_iff_Ici, continuousWithinAt_Iio_iff_Iic,
    continuousAt_iff_continuous_left_right]
#align continuous_at_iff_continuous_left'_right' continuousAt_iff_continuous_left'_right'

end TopologicalSpace
