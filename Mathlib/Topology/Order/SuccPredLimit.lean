/-
Copyright (c) 2024 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
module

public import Mathlib.Topology.Order.Basic
public import Mathlib.Order.SuccPred.Limit
import Mathlib.Topology.Order.IsLUB

public section

open Set Filter Order OrderDual
open scoped Topology

variable {X : Type*} [LinearOrder X] [TopologicalSpace X] [OrderTopology X]
  {s : Set X} {a b : X}

theorem comap_coe_nhdsLT_eq_atTop_iff :
    comap ((â†‘) : s â†’ X) (ğ“[<] b) = atTop â†”
      s âŠ† Iio b âˆ§ (s.Nonempty â†’ âˆ€ a < b, (s âˆ© Ioo a b).Nonempty) := by
  rcases s.eq_empty_or_nonempty with rfl | hsne
  Â· simp [eq_iff_true_of_subsingleton]
  have := hsne.to_subtype
  simp only [hsne, true_imp_iff]
  by_cases hsub : s âŠ† Iio b
  Â· simp only [hsub, true_and]
    constructor
    Â· intro h a ha
      have := preimage_mem_comap (m := ((â†‘) : s â†’ X)) (Ioo_mem_nhdsLT ha)
      rw [h] at this
      rcases Filter.nonempty_of_mem this with âŸ¨âŸ¨c, hcsâŸ©, hcâŸ©
      exact âŸ¨c, hcs, hcâŸ©
    Â· intro h
      refine (nhdsLT_basis_of_exists_lt (hsne.mono hsub)).comap _ |>.ext atTop_basis ?_ ?_
      Â· intro a hab
        rcases h a hab with âŸ¨c, hcs, hcâŸ©
        use âŸ¨c, hcsâŸ©
        simp_all [subset_def, hc.1.trans_le]
      Â· rintro âŸ¨a, hasâŸ© -
        use a, hsub has
        simp_all [subset_def, le_of_lt]
  Â· suffices Â¬Tendsto (â†‘) (atTop : Filter s) (ğ“[<] b) by
      contrapose this
      simp_all [tendsto_iff_comap]
    intro h
    rcases not_subset_iff_exists_mem_notMem.mp hsub with âŸ¨a, has, haâŸ©
    rcases h.eventually eventually_mem_nhdsWithin |>.and (eventually_ge_atTop âŸ¨a, hasâŸ©) |>.exists
      with âŸ¨âŸ¨c, hcsâŸ©, hcb, hacâŸ©
    apply lt_irrefl a
    calc
      a â‰¤ c := by simpa using hac
      _ < b := by simpa using hcb
      _ â‰¤ a := by simpa using ha

theorem comap_coe_nhdsGT_eq_atBot_iff :
    comap ((â†‘) : s â†’ X) (ğ“[>] b) = atBot â†”
      s âŠ† Ioi b âˆ§ (s.Nonempty â†’ âˆ€ a > b, (s âˆ© Ioo b a).Nonempty) := by
  refine comap_coe_nhdsLT_eq_atTop_iff (s := OrderDual.ofDual â»Â¹' s) (b := OrderDual.toDual b)
    |>.trans <| .and .rfl <| forallâ‚ƒ_congr fun hne a ha â†¦ ?_
  rw [â† a.toDual_ofDual, Ioo_toDual]
  rfl

theorem comap_coe_nhdsLT_of_Ioo_subset (hsb : s âŠ† Iio b) (hs : s.Nonempty â†’ âˆƒ a < b, Ioo a b âŠ† s)
    (hb : IsSuccPrelimit b) :
    comap ((â†‘) : s â†’ X) (ğ“[<] b) = atTop := by
  rw [comap_coe_nhdsLT_eq_atTop_iff]
  refine âŸ¨hsb, fun hsne a ha â†¦ ?_âŸ©
  rcases hs hsne with âŸ¨c, hcb, hcsâŸ©
  rcases hb.lt_iff_exists_lt.mp (max_lt ha hcb) with âŸ¨x, hxb, hacxâŸ©
  rw [max_lt_iff] at hacx
  exact âŸ¨x, hcs âŸ¨hacx.2, hxbâŸ©, hacx.1, hxbâŸ©

theorem comap_coe_nhdsGT_of_Ioo_subset (hsa : s âŠ† Ioi a) (hs : s.Nonempty â†’ âˆƒ b > a, Ioo a b âŠ† s)
    (ha : IsPredPrelimit a) :
    comap ((â†‘) : s â†’ X) (ğ“[>] a) = atBot := by
  refine comap_coe_nhdsLT_of_Ioo_subset (show ofDual â»Â¹' s âŠ† Iio (toDual a) from hsa) ?_ ha.dual
  simpa only [OrderDual.exists, Ioo_toDual]

theorem map_coe_atTop_of_Ioo_subset (hsb : s âŠ† Iio b) (hs : âˆ€ a' < b, âˆƒ a < b, Ioo a b âŠ† s)
    (hb : IsSuccPrelimit b) :
    map ((â†‘) : s â†’ X) atTop = ğ“[<] b := by
  rcases eq_empty_or_nonempty (Iio b) with (hb' | âŸ¨a, haâŸ©)
  Â· have : IsEmpty s := âŸ¨fun x => hb'.subset (hsb x.2)âŸ©
    rw [filter_eq_bot_of_isEmpty atTop, Filter.map_bot, hb', nhdsWithin_empty]
  Â· rw [â† comap_coe_nhdsLT_of_Ioo_subset hsb (fun _ => hs a ha) hb, map_comap_of_mem]
    rw [Subtype.range_val]
    exact (mem_nhdsLT_iff_exists_Ioo_subset' ha).2 (hs a ha)

theorem map_coe_atBot_of_Ioo_subset (hsa : s âŠ† Ioi a) (hs : âˆ€ b' > a, âˆƒ b > a, Ioo a b âŠ† s)
    (ha : IsPredPrelimit a) :
    map ((â†‘) : s â†’ X) atBot = ğ“[>] a := by
  refine map_coe_atTop_of_Ioo_subset (s := ofDual â»Â¹' s) (b := toDual a) hsa ?_ ha.dual
  intro b' hb'
  simpa [OrderDual.exists] using hs (ofDual b') hb'

/-- The `atTop` filter for an open interval `Ioo a b` comes from the left-neighbourhoods filter at
the right endpoint in the ambient order. -/
@[simp]
theorem comap_coe_Ioo_nhdsLT (a b : X) (hb : IsSuccPrelimit b) :
    comap ((â†‘) : Ioo a b â†’ X) (ğ“[<] b) = atTop :=
  comap_coe_nhdsLT_of_Ioo_subset Ioo_subset_Iio_self
    (fun h => âŸ¨a, h.elim fun _x hx â†¦ hx.1.trans hx.2, Subset.rflâŸ©) hb

/-- The `atBot` filter for an open interval `Ioo a b` comes from the right-neighbourhoods filter at
the left endpoint in the ambient order. -/
@[simp]
theorem comap_coe_Ioo_nhdsGT (a b : X) (ha : IsPredPrelimit a) :
    comap ((â†‘) : Ioo a b â†’ X) (ğ“[>] a) = atBot :=
  comap_coe_nhdsGT_of_Ioo_subset Ioo_subset_Ioi_self
    (fun h => âŸ¨b, h.elim fun _x hx â†¦ hx.1.trans hx.2, Subset.rflâŸ©) ha

@[simp]
theorem comap_coe_Ioi_nhdsGT (a : X) (ha : IsPredPrelimit a) :
    comap ((â†‘) : Ioi a â†’ X) (ğ“[>] a) = atBot :=
  comap_coe_nhdsGT_of_Ioo_subset Subset.rfl (fun âŸ¨x, hxâŸ© => âŸ¨x, hx, Ioo_subset_Ioi_selfâŸ©) ha

@[simp]
theorem comap_coe_Iio_nhdsLT (a : X) (ha : IsSuccPrelimit a) :
    comap ((â†‘) : Iio a â†’ X) (ğ“[<] a) = atTop :=
  comap_coe_Ioi_nhdsGT (toDual a) ha.dual

@[simp]
theorem map_coe_Ioo_atTop {a b : X} (h : a < b) (hb : IsSuccPrelimit b) :
    map ((â†‘) : Ioo a b â†’ X) atTop = ğ“[<] b :=
  map_coe_atTop_of_Ioo_subset Ioo_subset_Iio_self (fun _ _ => âŸ¨_, h, Subset.rflâŸ©) hb

@[simp]
theorem map_coe_Ioo_atBot {a b : X} (h : a < b) (ha : IsPredPrelimit a) :
    map ((â†‘) : Ioo a b â†’ X) atBot = ğ“[>] a :=
  map_coe_atBot_of_Ioo_subset Ioo_subset_Ioi_self (fun _ _ => âŸ¨_, h, Subset.rflâŸ©) ha

@[simp]
theorem map_coe_Ioi_atBot (a : X) (ha : IsPredPrelimit a) : map ((â†‘) : Ioi a â†’ X) atBot = ğ“[>] a :=
  map_coe_atBot_of_Ioo_subset Subset.rfl (fun b hb => âŸ¨b, hb, Ioo_subset_Ioi_selfâŸ©) ha

@[simp]
theorem map_coe_Iio_atTop (a : X) (ha : IsSuccPrelimit a) : map ((â†‘) : Iio a â†’ X) atTop = ğ“[<] a :=
  map_coe_Ioi_atBot (toDual a) ha.dual

variable {Î± : Type*} {l : Filter Î±} {f : X â†’ Î±}

@[simp]
theorem tendsto_comp_coe_Ioo_atTop (h : a < b) (hb : IsSuccPrelimit b) :
    Tendsto (fun x : Ioo a b => f x) atTop l â†” Tendsto f (ğ“[<] b) l := by
  rw [â† map_coe_Ioo_atTop h hb, tendsto_map'_iff, Function.comp_def]

@[simp]
theorem tendsto_comp_coe_Ioo_atBot (h : a < b) (ha : IsPredPrelimit a) :
    Tendsto (fun x : Ioo a b => f x) atBot l â†” Tendsto f (ğ“[>] a) l := by
  rw [â† map_coe_Ioo_atBot h ha, tendsto_map'_iff, Function.comp_def]

@[simp]
theorem tendsto_comp_coe_Ioi_atBot (ha : IsPredPrelimit a) :
    Tendsto (fun x : Ioi a => f x) atBot l â†” Tendsto f (ğ“[>] a) l := by
  rw [â† map_coe_Ioi_atBot a ha, tendsto_map'_iff, Function.comp_def]

@[simp]
theorem tendsto_comp_coe_Iio_atTop (ha : IsSuccPrelimit a) :
    Tendsto (fun x : Iio a => f x) atTop l â†” Tendsto f (ğ“[<] a) l := by
  rw [â† map_coe_Iio_atTop a ha, tendsto_map'_iff, Function.comp_def]

@[simp]
theorem tendsto_Ioo_atTop {f : Î± â†’ Ioo a b} (hb : IsSuccPrelimit b) :
    Tendsto f l atTop â†” Tendsto (fun x => (f x : X)) l (ğ“[<] b) := by
  rw [â† comap_coe_Ioo_nhdsLT a b hb, tendsto_comap_iff, Function.comp_def]

@[simp]
theorem tendsto_Ioo_atBot {f : Î± â†’ Ioo a b} (ha : IsPredPrelimit a) :
    Tendsto f l atBot â†” Tendsto (fun x => (f x : X)) l (ğ“[>] a) := by
  rw [â† comap_coe_Ioo_nhdsGT a b ha, tendsto_comap_iff, Function.comp_def]

@[simp]
theorem tendsto_Ioi_atBot {f : Î± â†’ Ioi a} (ha : IsPredPrelimit a) :
    Tendsto f l atBot â†” Tendsto (fun x => (f x : X)) l (ğ“[>] a) := by
  rw [â† comap_coe_Ioi_nhdsGT a ha, tendsto_comap_iff, Function.comp_def]

@[simp]
theorem tendsto_Iio_atTop {f : Î± â†’ Iio a} (ha : IsSuccPrelimit a) :
    Tendsto f l atTop â†” Tendsto (fun x => (f x : X)) l (ğ“[<] a) := by
  rw [â† comap_coe_Iio_nhdsLT a ha, tendsto_comap_iff, Function.comp_def]
