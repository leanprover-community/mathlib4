/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Order.Interval.Set.OrdConnectedComponent
import Mathlib.Topology.Order.Basic
import Mathlib.Topology.Separation.Regular

/-!
# Linear order is a completely normal Hausdorff topological space

In this file we prove that a linear order with order topology is a completely normal Hausdorff
topological space.
-/


open Filter Set Function OrderDual Topology Interval

variable {X : Type*} [LinearOrder X] [TopologicalSpace X] [OrderTopology X] {a : X} {s t : Set X}

namespace Set

@[simp]
theorem ordConnectedComponent_mem_nhds : ordConnectedComponent s a âˆˆ ğ“ a â†” s âˆˆ ğ“ a := by
  refine âŸ¨fun h â†¦ mem_of_superset h ordConnectedComponent_subset, fun h â†¦ ?_âŸ©
  rcases exists_Icc_mem_subset_of_mem_nhds h with âŸ¨b, c, ha, ha', hsâŸ©
  exact mem_of_superset ha' (subset_ordConnectedComponent ha hs)

theorem compl_ordConnectedSection_ordSeparatingSet_mem_nhdsGE (hd : Disjoint s (closure t))
    (ha : a âˆˆ s) : (ordConnectedSection (ordSeparatingSet s t))á¶œ âˆˆ ğ“[â‰¥] a := by
  have hmem : tá¶œ âˆˆ ğ“[â‰¥] a := by
    refine mem_nhdsWithin_of_mem_nhds ?_
    rw [â† mem_interior_iff_mem_nhds, interior_compl]
    exact disjoint_left.1 hd ha
  rcases exists_Icc_mem_subset_of_mem_nhdsGE hmem with âŸ¨b, hab, hmem', hsubâŸ©
  by_cases H : Disjoint (Icc a b) (ordConnectedSection <| ordSeparatingSet s t)
  Â· exact mem_of_superset hmem' (disjoint_left.1 H)
  Â· simp only [Set.disjoint_left, not_forall, Classical.not_not] at H
    rcases H with âŸ¨c, âŸ¨hac, hcbâŸ©, hcâŸ©
    have hsub' : Icc a b âŠ† ordConnectedComponent tá¶œ a :=
      subset_ordConnectedComponent (left_mem_Icc.2 hab) hsub
    have hd : Disjoint s (ordConnectedSection (ordSeparatingSet s t)) :=
      disjoint_left_ordSeparatingSet.mono_right ordConnectedSection_subset
    replace hac : a < c := hac.lt_of_ne <| Ne.symm <| ne_of_mem_of_not_mem hc <|
      disjoint_left.1 hd ha
    filter_upwards [Ico_mem_nhdsGE hac] with x hx hx'
    refine hx.2.ne (eq_of_mem_ordConnectedSection_of_uIcc_subset hx' hc ?_)
    refine subset_inter (subset_iUnionâ‚‚_of_subset a ha ?_) ?_
    Â· exact OrdConnected.uIcc_subset inferInstance (hsub' âŸ¨hx.1, hx.2.le.trans hcbâŸ©)
        (hsub' âŸ¨hac.le, hcbâŸ©)
    Â· rcases mem_iUnionâ‚‚.1 (ordConnectedSection_subset hx').2 with âŸ¨y, hyt, hxyâŸ©
      refine subset_iUnionâ‚‚_of_subset y hyt (OrdConnected.uIcc_subset inferInstance hxy ?_)
      refine subset_ordConnectedComponent left_mem_uIcc hxy ?_
      suffices c < y by
        rw [uIcc_of_ge (hx.2.trans this).le]
        exact âŸ¨hx.2.le, this.leâŸ©
      refine lt_of_not_ge fun hyc â†¦ ?_
      have hya : y < a := not_le.1 fun hay â†¦ hsub âŸ¨hay, hyc.trans hcbâŸ© hyt
      exact hxy (Icc_subset_uIcc âŸ¨hya.le, hx.1âŸ©) ha

theorem compl_ordConnectedSection_ordSeparatingSet_mem_nhdsLE (hd : Disjoint s (closure t))
    (ha : a âˆˆ s) : (ordConnectedSection <| ordSeparatingSet s t)á¶œ âˆˆ ğ“[â‰¤] a := by
  have hd' : Disjoint (ofDual â»Â¹' s) (closure <| ofDual â»Â¹' t) := hd
  have ha' : toDual a âˆˆ ofDual â»Â¹' s := ha
  simpa only [dual_ordSeparatingSet, dual_ordConnectedSection] using
    compl_ordConnectedSection_ordSeparatingSet_mem_nhdsGE hd' ha'

theorem compl_ordConnectedSection_ordSeparatingSet_mem_nhds (hd : Disjoint s (closure t))
    (ha : a âˆˆ s) : (ordConnectedSection <| ordSeparatingSet s t)á¶œ âˆˆ ğ“ a := by
  rw [â† nhdsLE_sup_nhdsGE, mem_sup]
  exact âŸ¨compl_ordConnectedSection_ordSeparatingSet_mem_nhdsLE hd ha,
    compl_ordConnectedSection_ordSeparatingSet_mem_nhdsGE hd haâŸ©

theorem ordT5Nhd_mem_nhdsSet (hd : Disjoint s (closure t)) : ordT5Nhd s t âˆˆ ğ“Ë¢ s :=
  bUnion_mem_nhdsSet fun x hx â†¦ ordConnectedComponent_mem_nhds.2 <| inter_mem
    (by
      rw [â† mem_interior_iff_mem_nhds, interior_compl]
      exact disjoint_left.1 hd hx)
    (compl_ordConnectedSection_ordSeparatingSet_mem_nhds hd hx)

end Set

open Set

/-- A linear order with order topology is a completely normal Hausdorff topological space. -/
instance (priority := 100) OrderTopology.completelyNormalSpace : CompletelyNormalSpace X :=
  âŸ¨fun s t hâ‚ hâ‚‚ â†¦ Filter.disjoint_iff.2
    âŸ¨ordT5Nhd s t, ordT5Nhd_mem_nhdsSet hâ‚‚, ordT5Nhd t s, ordT5Nhd_mem_nhdsSet hâ‚.symm,
      disjoint_ordT5NhdâŸ©âŸ©

instance (priority := 100) OrderTopology.t5Space : T5Space X := T5Space.mk
