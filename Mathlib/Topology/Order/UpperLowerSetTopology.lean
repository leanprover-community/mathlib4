/-
Copyright (c) 2023 Christopher Hoskin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christopher Hoskin
-/
module

public import Mathlib.Logic.Lemmas
public import Mathlib.Topology.AlexandrovDiscrete
public import Mathlib.Topology.ContinuousMap.Basic
public import Mathlib.Topology.Order.LowerUpperTopology

/-!
# Upper and lower sets topologies

This file introduces the upper set topology on a preorder as the topology where the open sets are
the upper sets and the lower set topology on a preorder as the topology where the open sets are
the lower sets.

In general the upper set topology does not coincide with the upper topology and the lower set
topology does not coincide with the lower topology.

## Main statements

- `Topology.IsUpperSet.toAlexandrovDiscrete`: The upper set topology is Alexandrov-discrete.
- `Topology.IsUpperSet.isClosed_iff_isLower` - a set is closed if and only if it is a Lower set
- `Topology.IsUpperSet.closure_eq_lowerClosure` - topological closure coincides with lower closure
- `Topology.IsUpperSet.monotone_iff_continuous` - the continuous functions are the monotone
  functions
- `IsUpperSet.monotone_to_upperTopology_continuous`: A monotone map from a preorder with the upper
  set topology to a preorder with the upper topology is continuous.

We provide the upper set topology in three ways (and similarly for the lower set topology):
* `Topology.upperSet`: The upper set topology as a `TopologicalSpace Œ±`
* `Topology.IsUpperSet`: Prop-valued mixin typeclass stating that an existing topology is the upper
  set topology.
* `Topology.WithUpperSet`: Type synonym equipping a preorder with its upper set topology.

## Motivation

An Alexandrov topology is a topology where the intersection of any collection of open sets is open.
The upper set topology is an Alexandrov topology and, given any Alexandrov topological space, we can
equip it with a preorder (namely the specialization preorder) whose upper set topology coincides
with the original topology. See `Topology.Specialization`.

## Tags

upper set topology, lower set topology, preorder, Alexandrov
-/

@[expose] public section

open Set TopologicalSpace Filter

variable {Œ± Œ≤ Œ≥ : Type*}

namespace Topology

/-- Topology whose open sets are upper sets.

Note: In general the upper set topology does not coincide with the upper topology. -/
def upperSet (Œ± : Type*) [Preorder Œ±] : TopologicalSpace Œ± where
  IsOpen := IsUpperSet
  isOpen_univ := isUpperSet_univ
  isOpen_inter _ _ := IsUpperSet.inter
  isOpen_sUnion _ := isUpperSet_sUnion

/-- Topology whose open sets are lower sets.

Note: In general the lower set topology does not coincide with the lower topology. -/
def lowerSet (Œ± : Type*) [Preorder Œ±] : TopologicalSpace Œ± where
  IsOpen := IsLowerSet
  isOpen_univ := isLowerSet_univ
  isOpen_inter _ _ := IsLowerSet.inter
  isOpen_sUnion _ := isLowerSet_sUnion

/-- Type synonym for a preorder equipped with the upper set topology. -/
def WithUpperSet (Œ± : Type*) := Œ±

namespace WithUpperSet

/-- `toUpperSet` is the identity function to the `WithUpperSet` of a type. -/
@[match_pattern] def toUpperSet : Œ± ‚âÉ WithUpperSet Œ± := Equiv.refl _

/-- `ofUpperSet` is the identity function from the `WithUpperSet` of a type. -/
@[match_pattern] def ofUpperSet : WithUpperSet Œ± ‚âÉ Œ± := Equiv.refl _

@[simp] lemma toUpperSet_symm : (@toUpperSet Œ±).symm = ofUpperSet := rfl
@[simp] lemma ofUpperSet_symm : (@ofUpperSet Œ±).symm = toUpperSet := rfl
@[simp] lemma toUpperSet_ofUpperSet (a : WithUpperSet Œ±) : toUpperSet (ofUpperSet a) = a := rfl
@[simp] lemma ofUpperSet_toUpperSet (a : Œ±) : ofUpperSet (toUpperSet a) = a := rfl
lemma toUpperSet_inj {a b : Œ±} : toUpperSet a = toUpperSet b ‚Üî a = b := Iff.rfl
lemma ofUpperSet_inj {a b : WithUpperSet Œ±} : ofUpperSet a = ofUpperSet b ‚Üî a = b := Iff.rfl

/-- A recursor for `WithUpperSet`. Use as `induction x`. -/
@[elab_as_elim, cases_eliminator, induction_eliminator]
protected def rec {Œ≤ : WithUpperSet Œ± ‚Üí Sort*} (h : ‚àÄ a, Œ≤ (toUpperSet a)) : ‚àÄ a, Œ≤ a :=
  fun a => h (ofUpperSet a)

instance [Nonempty Œ±] : Nonempty (WithUpperSet Œ±) := ‚ÄπNonempty Œ±‚Ä∫
instance [Inhabited Œ±] : Inhabited (WithUpperSet Œ±) := ‚ÄπInhabited Œ±‚Ä∫

variable [Preorder Œ±] [Preorder Œ≤]

instance : Preorder (WithUpperSet Œ±) := ‚ÄπPreorder Œ±‚Ä∫
instance : TopologicalSpace (WithUpperSet Œ±) := upperSet Œ±

lemma ofUpperSet_le_iff {a b : WithUpperSet Œ±} : ofUpperSet a ‚â§ ofUpperSet b ‚Üî a ‚â§ b := Iff.rfl
lemma toUpperSet_le_iff {a b : Œ±} : toUpperSet a ‚â§ toUpperSet b ‚Üî a ‚â§ b := Iff.rfl

/-- `ofUpperSet` as an `OrderIso` -/
def ofUpperSetOrderIso : WithUpperSet Œ± ‚âÉo Œ± where
  toEquiv := ofUpperSet
  map_rel_iff' := ofUpperSet_le_iff

/-- `toUpperSet` as an `OrderIso` -/
def toUpperSetOrderIso : Œ± ‚âÉo WithUpperSet Œ± where
  toEquiv := toUpperSet
  map_rel_iff' := toUpperSet_le_iff

end WithUpperSet

/-- Type synonym for a preorder equipped with the lower set topology. -/
def WithLowerSet (Œ± : Type*) := Œ±

namespace WithLowerSet

/-- `toLowerSet` is the identity function to the `WithLowerSet` of a type. -/
@[match_pattern] def toLowerSet : Œ± ‚âÉ WithLowerSet Œ± := Equiv.refl _

/-- `ofLowerSet` is the identity function from the `WithLowerSet` of a type. -/
@[match_pattern] def ofLowerSet : WithLowerSet Œ± ‚âÉ Œ± := Equiv.refl _

@[simp] lemma toLowerSet_symm : (@toLowerSet Œ±).symm = ofLowerSet := rfl
@[simp] lemma ofLowerSet_symm : (@ofLowerSet Œ±).symm = toLowerSet := rfl
@[simp] lemma toLowerSet_ofLowerSet (a : WithLowerSet Œ±) : toLowerSet (ofLowerSet a) = a := rfl
@[simp] lemma ofLowerSet_toLowerSet (a : Œ±) : ofLowerSet (toLowerSet a) = a := rfl
lemma toLowerSet_inj {a b : Œ±} : toLowerSet a = toLowerSet b ‚Üî a = b := Iff.rfl
lemma ofLowerSet_inj {a b : WithLowerSet Œ±} : ofLowerSet a = ofLowerSet b ‚Üî a = b := Iff.rfl

/-- A recursor for `WithLowerSet`. Use as `induction x`. -/
@[elab_as_elim, cases_eliminator, induction_eliminator]
protected def rec {Œ≤ : WithLowerSet Œ± ‚Üí Sort*} (h : ‚àÄ a, Œ≤ (toLowerSet a)) : ‚àÄ a, Œ≤ a :=
  fun a => h (ofLowerSet a)

instance [Nonempty Œ±] : Nonempty (WithLowerSet Œ±) := ‚ÄπNonempty Œ±‚Ä∫
instance [Inhabited Œ±] : Inhabited (WithLowerSet Œ±) := ‚ÄπInhabited Œ±‚Ä∫

variable [Preorder Œ±]

instance : Preorder (WithLowerSet Œ±) := ‚ÄπPreorder Œ±‚Ä∫
instance : TopologicalSpace (WithLowerSet Œ±) := lowerSet Œ±

lemma ofLowerSet_le_iff {a b : WithLowerSet Œ±} : ofLowerSet a ‚â§ ofLowerSet b ‚Üî a ‚â§ b := Iff.rfl
lemma toLowerSet_le_iff {a b : Œ±} : toLowerSet a ‚â§ toLowerSet b ‚Üî a ‚â§ b := Iff.rfl

/-- `ofLowerSet` as an `OrderIso` -/
def ofLowerSetOrderIso : WithLowerSet Œ± ‚âÉo Œ± where
  toEquiv := ofLowerSet
  map_rel_iff' := ofLowerSet_le_iff

/-- `toLowerSet` as an `OrderIso` -/
def toLowerSetOrderIso : Œ± ‚âÉo WithLowerSet Œ± where
  toEquiv := toLowerSet
  map_rel_iff' := toLowerSet_le_iff

end WithLowerSet

/--
The Upper Set topology is homeomorphic to the Lower Set topology on the dual order
-/
def WithUpperSet.toDualHomeomorph [Preorder Œ±] : WithUpperSet Œ± ‚âÉ‚Çú WithLowerSet Œ±·µí·µà where
  toFun := OrderDual.toDual
  invFun := OrderDual.ofDual
  left_inv := OrderDual.toDual_ofDual
  right_inv := OrderDual.ofDual_toDual
  continuous_toFun := continuous_coinduced_rng
  continuous_invFun := continuous_coinduced_rng


/--
The Lower Set topology is homeomorphic to the Upper Set topology on the dual order
-/
def WithLowerSet.toDualHomeomorph [Preorder Œ±] : WithLowerSet Œ± ‚âÉ‚Çú WithUpperSet Œ±·µí·µà where
  toFun := OrderDual.toDual
  invFun := OrderDual.ofDual
  left_inv := OrderDual.toDual_ofDual
  right_inv := OrderDual.ofDual_toDual
  continuous_toFun := continuous_coinduced_rng
  continuous_invFun := continuous_coinduced_rng

/-- Prop-valued mixin for an ordered topological space to be
The upper set topology is the topology where the open sets are the upper sets. In general the upper
set topology does not coincide with the upper topology.
-/
protected class IsUpperSet (Œ± : Type*) [t : TopologicalSpace Œ±] [Preorder Œ±] : Prop where
  topology_eq_upperSetTopology : t = upperSet Œ±

attribute [nolint docBlame] IsUpperSet.topology_eq_upperSetTopology

instance [Preorder Œ±] : Topology.IsUpperSet (WithUpperSet Œ±) := ‚ü®rfl‚ü©

instance [Preorder Œ±] : @Topology.IsUpperSet Œ± (upperSet Œ±) _ := by
  letI := upperSet Œ±
  exact ‚ü®rfl‚ü©

/--
The lower set topology is the topology where the open sets are the lower sets. In general the lower
set topology does not coincide with the lower topology.
-/
protected class IsLowerSet (Œ± : Type*) [t : TopologicalSpace Œ±] [Preorder Œ±] : Prop where
  topology_eq_lowerSetTopology : t = lowerSet Œ±

attribute [nolint docBlame] IsLowerSet.topology_eq_lowerSetTopology

instance [Preorder Œ±] : Topology.IsLowerSet (WithLowerSet Œ±) := ‚ü®rfl‚ü©

instance [Preorder Œ±] : @Topology.IsLowerSet Œ± (lowerSet Œ±) _ := by
  letI := lowerSet Œ±
  exact ‚ü®rfl‚ü©

namespace IsUpperSet

section Preorder

variable (Œ±)
variable [Preorder Œ±] [TopologicalSpace Œ±] [Topology.IsUpperSet Œ±] {s : Set Œ±}

lemma topology_eq : ‚Äπ_‚Ä∫ = upperSet Œ± := topology_eq_upperSetTopology

variable {Œ±}

instance _root_.OrderDual.instIsLowerSet [Preorder Œ±] [TopologicalSpace Œ±] [Topology.IsUpperSet Œ±] :
    Topology.IsLowerSet Œ±·µí·µà where
  topology_eq_lowerSetTopology := by ext; rw [IsUpperSet.topology_eq Œ±]

/-- If `Œ±` is equipped with the upper set topology, then it is homeomorphic to
`WithUpperSet Œ±`. -/
def WithUpperSetHomeomorph : WithUpperSet Œ± ‚âÉ‚Çú Œ± :=
  WithUpperSet.ofUpperSet.toHomeomorphOfIsInducing ‚ü®topology_eq Œ± ‚ñ∏ induced_id.symm‚ü©

lemma isOpen_iff_isUpperSet : IsOpen s ‚Üî IsUpperSet s := by
  rw [topology_eq Œ±]
  rfl

instance toAlexandrovDiscrete : AlexandrovDiscrete Œ± where
  isOpen_sInter S := by simpa only [isOpen_iff_isUpperSet] using isUpperSet_sInter (Œ± := Œ±)

-- c.f. isClosed_iff_lower_and_subset_implies_LUB_mem
lemma isClosed_iff_isLower : IsClosed s ‚Üî IsLowerSet s := by
  rw [‚Üê isOpen_compl_iff, isOpen_iff_isUpperSet,
    isLowerSet_compl.symm, compl_compl]

lemma closure_eq_lowerClosure {s : Set Œ±} : closure s = lowerClosure s := by
  rw [subset_antisymm_iff]
  refine ‚ü®?_, lowerClosure_min subset_closure (isClosed_iff_isLower.1 isClosed_closure)‚ü©
  ¬∑ apply closure_minimal subset_lowerClosure _
    rw [isClosed_iff_isLower]
    exact LowerSet.lower (lowerClosure s)

/--
The closure of a singleton `{a}` in the upper set topology is the right-closed left-infinite
interval (-‚àû,a].
-/
@[simp] lemma closure_singleton {a : Œ±} : closure {a} = Iic a := by
  rw [closure_eq_lowerClosure, lowerClosure_singleton]
  rfl

lemma specializes_iff_le {a b : Œ±} : a ‚§≥ b ‚Üî b ‚â§ a := by
  simp only [specializes_iff_closure_subset, closure_singleton, Iic_subset_Iic]

lemma nhdsKer_eq_upperClosure (s : Set Œ±) : nhdsKer s = ‚Üë(upperClosure s) := by
  ext; simp [mem_nhdsKer_iff_specializes, specializes_iff_le]

@[simp] lemma nhdsKer_singleton (a : Œ±) : nhdsKer {a} = Ici a := by
  rw [nhdsKer_eq_upperClosure, upperClosure_singleton, UpperSet.coe_Ici]

lemma nhds_eq_principal_Ici (a : Œ±) : ùìù a = ùìü (Ici a) := by
  rw [‚Üê principal_nhdsKer_singleton, nhdsKer_singleton]

lemma nhdsSet_eq_principal_upperClosure (s : Set Œ±) : ùìùÀ¢ s = ùìü ‚Üë(upperClosure s) := by
  rw [‚Üê principal_nhdsKer, nhdsKer_eq_upperClosure]

end Preorder

protected lemma _root_.Topology.isUpperSet_iff_nhds {Œ± : Type*} [TopologicalSpace Œ±] [Preorder Œ±] :
    Topology.IsUpperSet Œ± ‚Üî (‚àÄ a : Œ±, ùìù a = ùìü (Ici a)) where
  mp _ a := nhds_eq_principal_Ici a
  mpr hŒ± := ‚ü®by simp [TopologicalSpace.ext_iff_nhds, hŒ±, nhds_eq_principal_Ici]‚ü©

instance : Topology.IsUpperSet Prop := by
  simp [Topology.isUpperSet_iff_nhds, Prop.forall]

section maps

variable [Preorder Œ±] [Preorder Œ≤]

open Topology

protected lemma monotone_iff_continuous [TopologicalSpace Œ±] [TopologicalSpace Œ≤]
    [Topology.IsUpperSet Œ±] [Topology.IsUpperSet Œ≤] {f : Œ± ‚Üí Œ≤} : Monotone f ‚Üî Continuous f := by
  constructor
  ¬∑ intro hf
    simp_rw [continuous_def, isOpen_iff_isUpperSet]
    exact fun _ hs ‚Ü¶ IsUpperSet.preimage hs hf
  ¬∑ intro hf a b hab
    rw [‚Üê mem_Iic, ‚Üê closure_singleton] at hab ‚ä¢
    apply Continuous.closure_preimage_subset hf {f b}
    apply mem_of_mem_of_subset hab
    apply closure_mono
    rw [singleton_subset_iff, mem_preimage, mem_singleton_iff]

lemma monotone_to_upperTopology_continuous [TopologicalSpace Œ±] [TopologicalSpace Œ≤]
    [Topology.IsUpperSet Œ±] [IsUpper Œ≤] {f : Œ± ‚Üí Œ≤} (hf : Monotone f) : Continuous f := by
  simp_rw [continuous_def, isOpen_iff_isUpperSet]
  intro s hs
  exact (IsUpper.isUpperSet_of_isOpen hs).preimage hf

lemma upperSet_le_upper {t‚ÇÅ t‚ÇÇ : TopologicalSpace Œ±} [@Topology.IsUpperSet Œ± t‚ÇÅ _]
    [@Topology.IsUpper Œ± t‚ÇÇ _] : t‚ÇÅ ‚â§ t‚ÇÇ := fun s hs => by
  rw [@isOpen_iff_isUpperSet Œ± _ t‚ÇÅ]
  exact IsUpper.isUpperSet_of_isOpen hs

end maps

end IsUpperSet

namespace IsLowerSet

section Preorder

variable (Œ±)
variable [Preorder Œ±] [TopologicalSpace Œ±] [Topology.IsLowerSet Œ±] {s : Set Œ±}

lemma topology_eq : ‚Äπ_‚Ä∫ = lowerSet Œ± := topology_eq_lowerSetTopology

variable {Œ±}

instance _root_.OrderDual.instIsUpperSet [Preorder Œ±] [TopologicalSpace Œ±] [Topology.IsLowerSet Œ±] :
    Topology.IsUpperSet Œ±·µí·µà where
  topology_eq_upperSetTopology := by ext; rw [IsLowerSet.topology_eq Œ±]

/-- If `Œ±` is equipped with the lower set topology, then it is homeomorphic to `WithLowerSet Œ±`. -/
def WithLowerSetHomeomorph : WithLowerSet Œ± ‚âÉ‚Çú Œ± :=
  WithLowerSet.ofLowerSet.toHomeomorphOfIsInducing ‚ü®topology_eq Œ± ‚ñ∏ induced_id.symm‚ü©

lemma isOpen_iff_isLowerSet : IsOpen s ‚Üî IsLowerSet s := by rw [topology_eq Œ±]; rfl

instance toAlexandrovDiscrete : AlexandrovDiscrete Œ± := IsUpperSet.toAlexandrovDiscrete (Œ± := Œ±·µí·µà)

lemma isClosed_iff_isUpper : IsClosed s ‚Üî IsUpperSet s := by
  rw [‚Üê isOpen_compl_iff, isOpen_iff_isLowerSet, isUpperSet_compl.symm, compl_compl]

lemma closure_eq_upperClosure {s : Set Œ±} : closure s = upperClosure s :=
  IsUpperSet.closure_eq_lowerClosure (Œ± := Œ±·µí·µà)

/--
The closure of a singleton `{a}` in the lower set topology is the right-closed left-infinite
interval (-‚àû,a].
-/
@[simp] lemma closure_singleton {a : Œ±} : closure {a} = Ici a := by
  rw [closure_eq_upperClosure, upperClosure_singleton]
  rfl

lemma specializes_iff_le {a b : Œ±} : a ‚§≥ b ‚Üî a ‚â§ b := by
  simp only [specializes_iff_closure_subset, closure_singleton, Ici_subset_Ici]

lemma nhdsKer_eq_lowerClosure (s : Set Œ±) : nhdsKer s = ‚Üë(lowerClosure s) := by
  ext; simp [mem_nhdsKer_iff_specializes, specializes_iff_le]

@[simp] lemma nhdsKer_singleton (a : Œ±) : nhdsKer {a} = Iic a := by
  rw [nhdsKer_eq_lowerClosure, lowerClosure_singleton, LowerSet.coe_Iic]

lemma nhds_eq_principal_Iic (a : Œ±) : ùìù a = ùìü (Iic a) := by
  rw [‚Üê principal_nhdsKer_singleton, nhdsKer_singleton]

lemma nhdsSet_eq_principal_lowerClosure (s : Set Œ±) : ùìùÀ¢ s = ùìü ‚Üë(lowerClosure s) := by
  rw [‚Üê principal_nhdsKer, nhdsKer_eq_lowerClosure]

end Preorder

protected lemma _root_.Topology.isLowerSet_iff_nhds {Œ± : Type*} [TopologicalSpace Œ±] [Preorder Œ±] :
    Topology.IsLowerSet Œ± ‚Üî (‚àÄ a : Œ±, ùìù a = ùìü (Iic a)) where
  mp _ a := nhds_eq_principal_Iic a
  mpr hŒ± := ‚ü®by simp [TopologicalSpace.ext_iff_nhds, hŒ±, nhds_eq_principal_Iic]‚ü©

section maps

variable [Preorder Œ±] [Preorder Œ≤]

open Topology
open OrderDual

protected lemma monotone_iff_continuous [TopologicalSpace Œ±] [TopologicalSpace Œ≤]
    [Topology.IsLowerSet Œ±] [Topology.IsLowerSet Œ≤] {f : Œ± ‚Üí Œ≤} : Monotone f ‚Üî Continuous f := by
  rw [‚Üê monotone_dual_iff]
  exact IsUpperSet.monotone_iff_continuous (Œ± := Œ±·µí·µà) (Œ≤ := Œ≤·µí·µà)
    (f := (toDual ‚àò f ‚àò ofDual : Œ±·µí·µà ‚Üí Œ≤·µí·µà))

lemma monotone_to_lowerTopology_continuous [TopologicalSpace Œ±] [TopologicalSpace Œ≤]
    [Topology.IsLowerSet Œ±] [IsLower Œ≤] {f : Œ± ‚Üí Œ≤} (hf : Monotone f) : Continuous f :=
  IsUpperSet.monotone_to_upperTopology_continuous (Œ± := Œ±·µí·µà) (Œ≤ := Œ≤·µí·µà) hf.dual

lemma lowerSet_le_lower {t‚ÇÅ t‚ÇÇ : TopologicalSpace Œ±} [@Topology.IsLowerSet Œ± t‚ÇÅ _]
    [@IsLower Œ± t‚ÇÇ _] : t‚ÇÅ ‚â§ t‚ÇÇ := fun s hs => by
  rw [@isOpen_iff_isLowerSet Œ± _ t‚ÇÅ]
  exact IsLower.isLowerSet_of_isOpen hs

end maps

end IsLowerSet

lemma isUpperSet_orderDual [Preorder Œ±] [TopologicalSpace Œ±] :
    Topology.IsUpperSet Œ±·µí·µà ‚Üî Topology.IsLowerSet Œ± := by
  constructor
  ¬∑ apply OrderDual.instIsLowerSet
  ¬∑ apply OrderDual.instIsUpperSet

lemma isLowerSet_orderDual [Preorder Œ±] [TopologicalSpace Œ±] :
    Topology.IsLowerSet Œ±·µí·µà ‚Üî Topology.IsUpperSet Œ± := isUpperSet_orderDual.symm

namespace WithUpperSet
variable [Preorder Œ±] [Preorder Œ≤] [Preorder Œ≥]

/-- A monotone map between preorders spaces induces a continuous map between themselves considered
with the upper set topology. -/
def map (f : Œ± ‚Üío Œ≤) : C(WithUpperSet Œ±, WithUpperSet Œ≤) where
  toFun := toUpperSet ‚àò f ‚àò ofUpperSet
  continuous_toFun := continuous_def.2 fun _s hs ‚Ü¶ IsUpperSet.preimage hs f.monotone

@[simp] lemma map_id : map (OrderHom.id : Œ± ‚Üío Œ±) = ContinuousMap.id _ := rfl
@[simp] lemma map_comp (g : Œ≤ ‚Üío Œ≥) (f : Œ± ‚Üío Œ≤) : map (g.comp f) = (map g).comp (map f) := rfl

@[simp] lemma toUpperSet_specializes_toUpperSet {a b : Œ±} :
    toUpperSet a ‚§≥ toUpperSet b ‚Üî b ‚â§ a := by
  simp_rw [specializes_iff_closure_subset, IsUpperSet.closure_singleton, Iic_subset_Iic,
    toUpperSet_le_iff]

@[simp] lemma ofUpperSet_le_ofUpperSet {a b : WithUpperSet Œ±} :
    ofUpperSet a ‚â§ ofUpperSet b ‚Üî b ‚§≥ a := toUpperSet_specializes_toUpperSet.symm

@[simp] lemma isUpperSet_toUpperSet_preimage {s : Set (WithUpperSet Œ±)} :
    IsUpperSet (toUpperSet ‚Åª¬π' s) ‚Üî IsOpen s := Iff.rfl

@[simp] lemma isOpen_ofUpperSet_preimage {s : Set Œ±} :
    IsOpen (ofUpperSet ‚Åª¬π' s) ‚Üî IsUpperSet s := isUpperSet_toUpperSet_preimage.symm

end WithUpperSet

namespace WithLowerSet
variable [Preorder Œ±] [Preorder Œ≤] [Preorder Œ≥]

/-- A monotone map between preorders spaces induces a continuous map between themselves considered
with the lower set topology. -/
def map (f : Œ± ‚Üío Œ≤) : C(WithLowerSet Œ±, WithLowerSet Œ≤) where
  toFun := toLowerSet ‚àò f ‚àò ofLowerSet
  continuous_toFun := continuous_def.2 fun _s hs ‚Ü¶ IsLowerSet.preimage hs f.monotone

@[simp] lemma map_id : map (OrderHom.id : Œ± ‚Üío Œ±) = ContinuousMap.id _ := rfl
@[simp] lemma map_comp (g : Œ≤ ‚Üío Œ≥) (f : Œ± ‚Üío Œ≤) : map (g.comp f) = (map g).comp (map f) := rfl

@[simp] lemma toLowerSet_specializes_toLowerSet {a b : Œ±} :
    toLowerSet a ‚§≥ toLowerSet b ‚Üî a ‚â§ b := by
  simp_rw [specializes_iff_closure_subset, IsLowerSet.closure_singleton, Ici_subset_Ici,
    toLowerSet_le_iff]

@[simp] lemma ofLowerSet_le_ofLowerSet {a b : WithLowerSet Œ±} :
    ofLowerSet a ‚â§ ofLowerSet b ‚Üî a ‚§≥ b := toLowerSet_specializes_toLowerSet.symm

@[simp] lemma isLowerSet_toLowerSet_preimage {s : Set (WithLowerSet Œ±)} :
    IsLowerSet (toLowerSet ‚Åª¬π' s) ‚Üî IsOpen s := Iff.rfl

@[simp] lemma isOpen_ofLowerSet_preimage {s : Set Œ±} :
    IsOpen (ofLowerSet ‚Åª¬π' s) ‚Üî IsLowerSet s := isLowerSet_toLowerSet_preimage.symm

end WithLowerSet

namespace IsUpperSet
variable [Preorder Œ±] [TopologicalSpace Œ±] [Topology.IsUpperSet Œ±]
    [Preorder Œ≤] [TopologicalSpace Œ≤] [Topology.IsUpperSet Œ≤]

open scoped Filter

lemma specializes_bot [OrderBot Œ±] {a : Œ±} : a ‚§≥ ‚ä• := by
  simp [IsUpperSet.specializes_iff_le]

lemma specializes_top [OrderTop Œ±] {a : Œ±} : ‚ä§ ‚§≥ a := by
  simp [IsUpperSet.specializes_iff_le]

@[simps]
instance [OrderBot Œ±] : OrderBot (WithUpperSet Œ±) where
  bot := WithUpperSet.toUpperSet ‚ä•
  bot_le a := by cases a; simp [WithUpperSet.toUpperSet_le_iff]

@[simps]
instance [OrderTop Œ±] : OrderTop (WithUpperSet Œ±) where
  top := WithUpperSet.toUpperSet ‚ä§
  le_top a := by cases a; simp [WithUpperSet.toUpperSet_le_iff]

lemma WithBot.continuous_coe :
    Continuous (Y := WithUpperSet <| WithBot Œ±) (WithUpperSet.toUpperSet ‚àò WithBot.some) := by
  rw [‚Üê IsUpperSet.monotone_iff_continuous]
  exact WithBot.coe_mono

lemma WithTop.continuous_coe :
    Continuous (Y := WithUpperSet <| WithTop Œ±) (WithUpperSet.toUpperSet ‚àò WithTop.some) := by
  rw [‚Üê IsUpperSet.monotone_iff_continuous]
  exact WithTop.coe_mono

lemma WithBot.isOpenEmbedding_coe :
    IsOpenEmbedding (Y := WithUpperSet <| WithBot Œ±) (WithUpperSet.toUpperSet ‚àò WithBot.some) :=
  have inj : (WithUpperSet.toUpperSet ‚àò WithBot.some).Injective := Option.some_injective _
{ eq_induced := by
    ext s
    simp_rw [isOpen_induced_iff]
    constructor
    ¬∑ intro hs; use WithUpperSet.toUpperSet ‚àò WithBot.some '' s; split_ands
      ¬∑ rw [IsUpperSet.isOpen_iff_isUpperSet, IsUpperSet]
        intro a b; cases a with | _ a => cases b with | _ b =>
        cases a using WithBot.recBotCoe <;> cases b using WithBot.recBotCoe
        rotate_right
        ¬∑ simp_rw [WithUpperSet.toUpperSet_le_iff, WithBot.coe_le_coe,
           ‚Üê IsUpperSet.specializes_iff_le]; intro h
          simp_rw [‚Üê Function.comp_apply (f := WithUpperSet.toUpperSet), inj.mem_set_image]
          exact h.mem_open hs
        all_goals simp [WithBot.some, WithUpperSet, WithUpperSet.toUpperSet, WithBot.le_bot_iff]
      ¬∑ rw [inj.preimage_image]
    ¬∑ rintro ‚ü®t, tO, rfl‚ü©
      exact tO.preimage WithBot.continuous_coe
  injective := inj
  isOpen_range := by
    rw [‚Üê isClosed_compl_iff]
    convert_to IsClosed {(‚ä• : WithUpperSet (WithBot Œ±))}
    ¬∑ ext x
      cases x using WithBot.recBotCoe <;> simp [WithUpperSet, WithUpperSet.toUpperSet]
    ¬∑ rw [IsUpperSet.isClosed_iff_isLower, IsLowerSet]
      rintro a b h ‚ü®‚ü©; simpa [WithUpperSet, WithBot.le_bot_iff] using h }

lemma nhds_bot [OrderBot Œ±] : ùìù (‚ä• : Œ±) = ‚ä§ := by
  rw [eq_top_iff, le_nhds_iff]
  intro s hs sO
  rw [Filter.mem_top, eq_univ_iff_forall]
  intro x; exact specializes_bot.mem_open sO hs

omit [TopologicalSpace Œ±] in
lemma WithBot.isClosed_singleton_bot : IsClosed {(‚ä• : WithUpperSet <| WithBot Œ±)} := by
  rw [IsUpperSet.isClosed_iff_isLower, IsLowerSet]
  rintro x y h ‚ü®‚ü©; cases y
  simp [WithUpperSet.toUpperSet_le_iff, WithBot.le_bot_iff] at h; simp [h]

omit [TopologicalSpace Œ±] in
@[simp]
lemma WithBot.le_bot_iff {a : WithUpperSet (WithBot Œ±)} :
    a ‚â§ WithUpperSet.toUpperSet (‚ä• : WithBot Œ±) ‚Üî a = WithUpperSet.toUpperSet (‚ä• : WithBot Œ±) :=
  _root_.WithBot.le_bot_iff

def WithBot.lift {X} [TopologicalSpace X] {U : Set X} [DecidablePred (¬∑ ‚àà U)] (Uo : IsOpen U)
    (f : C(U, Œ±)) : C(X, WithUpperSet (WithBot Œ±)) where
  toFun x := if h : x ‚àà U then (WithUpperSet.toUpperSet ‚àò WithBot.some) (f ‚ü®x, h‚ü©) else ‚ä•
  continuous_toFun := by
    constructor; intro s hs
    by_cases hb : ‚ä• ‚àà s
    ¬∑ have : s = univ := by
        rw [eq_univ_iff_forall]; intro x; exact IsUpperSet.specializes_bot.mem_open hs hb
      simp [this]
    ¬∑ simp only [preimage_dif, hb, exists_false, setOf_false, union_empty]
      rw [Uo.isOpenEmbedding_subtypeVal.isOpen_iff_preimage_isOpen, preimage_setOf_eq]
      ¬∑ simpa [‚Üê mem_preimage, setOf_mem_eq] using
          hs.preimage WithBot.continuous_coe |>.preimage <| map_continuous f
      ¬∑ intro x; simp +contextual

@[simp]
lemma WithBot.lift_coe {X} [TopologicalSpace X] {U : Set X} [DecidablePred (¬∑ ‚àà U)] (Uo : IsOpen U)
    (f : C(U, Œ±)) (x : U) :
    WithBot.lift Uo f (x : X) = (WithUpperSet.toUpperSet ‚àò WithBot.some) (f x) := by
  simp [WithBot.lift]

@[simp]
lemma WithBot.lift_of_mem {X} [TopologicalSpace X] {U : Set X} [DecidablePred (¬∑ ‚àà U)]
    (Uo : IsOpen U) (f : C(U, Œ±)) {x : X} (hx : x ‚àà U) :
    WithBot.lift Uo f x = (WithUpperSet.toUpperSet ‚àò WithBot.some) (f ‚ü®x, hx‚ü©) := by
  simp [WithBot.lift, hx]

@[simp]
lemma WithBot.lift_of_notMem {X} [TopologicalSpace X] {U : Set X} [DecidablePred (¬∑ ‚àà U)]
    (Uo : IsOpen U) (f : C(U, Œ±)) {x : X} (hx : x ‚àâ U) : WithBot.lift Uo f x = ‚ä• := by
  simp [WithBot.lift, hx]

@[simp]
lemma WithBot.lift_restrict {X} [TopologicalSpace X] {U : Set X} [DecidablePred (¬∑ ‚àà U)]
    (Uo : IsOpen U) (f : C(U, Œ±)) :
    (WithBot.lift Uo f).restrict U =
      .comp ‚ü®WithUpperSet.toUpperSet ‚àò WithBot.some, continuous_coe‚ü© f := by
  ext x; simp [WithBot.lift]

@[simp]
lemma WithBot.lift_restrict_compl {X} [TopologicalSpace X] {U : Set X} [DecidablePred (¬∑ ‚àà U)]
    (Uo : IsOpen U) (f : C(U, Œ±)) :
    (WithBot.lift Uo f).restrict U·∂ú = .const _ ‚ä• := by
  ext x; simpa [WithBot.lift, -Subtype.coe_prop] using x.2

end IsUpperSet

namespace IsLowerSet

variable [Preorder Œ±] [TopologicalSpace Œ±] [Topology.IsLowerSet Œ±]
    [Preorder Œ≤] [TopologicalSpace Œ≤] [Topology.IsLowerSet Œ≤]

lemma specializes_bot [OrderBot Œ±] {a : Œ±} : ‚ä• ‚§≥ a := by
  simp [IsLowerSet.specializes_iff_le]

lemma specializes_top [OrderTop Œ±] {a : Œ±} : a ‚§≥ ‚ä§ := by
  simp [IsLowerSet.specializes_iff_le]

@[simps]
instance [OrderBot Œ±] : OrderBot (WithLowerSet Œ±) where
  bot := WithLowerSet.toLowerSet ‚ä•
  bot_le a := by cases a; simp [WithLowerSet.toLowerSet_le_iff]

@[simps]
instance [OrderTop Œ±] : OrderTop (WithLowerSet Œ±) where
  top := WithLowerSet.toLowerSet ‚ä§
  le_top a := by cases a; simp [WithLowerSet.toLowerSet_le_iff]

lemma WithTop.continuous_coe :
    Continuous (Y := WithLowerSet <| WithTop Œ±) (WithLowerSet.toLowerSet ‚àò WithTop.some) := by
  rw [‚Üê IsLowerSet.monotone_iff_continuous]
  exact WithTop.coe_mono

lemma WithBot.continuous_coe :
    Continuous (Y := WithLowerSet <| WithBot Œ±) (WithLowerSet.toLowerSet ‚àò WithBot.some) := by
  rw [‚Üê IsLowerSet.monotone_iff_continuous]
  exact WithBot.coe_mono

open OrderDual in
lemma isOpenEmbedding_iff_orderDual {f : Œ± ‚Üí Œ≤} :
    IsOpenEmbedding f ‚Üî IsOpenEmbedding (toDual ‚àò f ‚àò ofDual) := by
  let Œ∑‚ÇÅ : Œ± ‚âÉ‚Çú Œ±·µí·µà :=
    IsLowerSet.WithLowerSetHomeomorph.symm.trans <|
      WithLowerSet.toDualHomeomorph.trans IsUpperSet.WithUpperSetHomeomorph
  let Œ∑‚ÇÇ : Œ≤ ‚âÉ‚Çú Œ≤·µí·µà :=
    IsLowerSet.WithLowerSetHomeomorph.symm.trans <|
      WithLowerSet.toDualHomeomorph.trans IsUpperSet.WithUpperSetHomeomorph
  have h_of : IsOpenEmbedding (@ofDual Œ±) := Œ∑‚ÇÅ.symm.isOpenEmbedding
  have h_to : IsOpenEmbedding (@toDual Œ≤) := Œ∑‚ÇÇ.isOpenEmbedding
  refine (fun (mp : {f : _} ‚Üí IsOpenEmbedding f ‚Üí  IsOpenEmbedding (‚áëtoDual ‚àò f ‚àò ‚áëofDual)) ‚Ü¶
    ‚ü®mp, ?mpr‚ü©) ?mp
  case mp => intro f h; exact h_to.comp (h.comp h_of)
  case mpr => intro h; simpa using mp h

lemma WithTop.isOpenEmbedding_coe :
    IsOpenEmbedding (Y := WithLowerSet <| WithTop Œ±) (WithLowerSet.toLowerSet ‚àò WithTop.some) := by
  rw [isOpenEmbedding_iff_orderDual]
  exact IsUpperSet.WithBot.isOpenEmbedding_coe

lemma nhds_top [OrderTop Œ±] : ùìù (‚ä§ : Œ±) = ‚ä§ := by
  rw [eq_top_iff, le_nhds_iff]
  intro s hs sO
  rw [Filter.mem_top, eq_univ_iff_forall]
  intro x; exact specializes_top.mem_open sO hs

omit [TopologicalSpace Œ±] in
lemma WithTop.isClosed_singleton_top : IsClosed {(‚ä§ : WithLowerSet <| WithTop Œ±)} := by
  rw [IsLowerSet.isClosed_iff_isUpper, IsUpperSet]
  rintro x y h ‚ü®‚ü©; cases y
  simp [WithLowerSet.toLowerSet_le_iff] at h; simp [h]

omit [TopologicalSpace Œ±] in
@[simp]
lemma WithTop.top_le_iff {a : WithLowerSet (WithTop Œ±)} :
    WithLowerSet.toLowerSet (‚ä§ : WithTop Œ±) ‚â§ a ‚Üî a = WithLowerSet.toLowerSet (‚ä§ : WithTop Œ±) :=
  _root_.WithTop.top_le_iff

def WithTop.lift {X} [TopologicalSpace X] {U : Set X} [DecidablePred (¬∑ ‚àà U)] (Uo : IsOpen U)
    (f : C(U, Œ±)) : C(X, WithLowerSet (WithTop Œ±)) where
  toFun x := if h : x ‚àà U then (WithLowerSet.toLowerSet ‚àò WithTop.some) (f ‚ü®x, h‚ü©) else ‚ä§
  continuous_toFun := by
    constructor; intro s hs
    by_cases hb : ‚ä§ ‚àà s
    ¬∑ have : s = univ := by
        rw [eq_univ_iff_forall]; intro x; exact IsLowerSet.specializes_top.mem_open hs hb
      simp [this]
    ¬∑ simp only [preimage_dif, hb, exists_false, setOf_false, union_empty]
      rw [Uo.isOpenEmbedding_subtypeVal.isOpen_iff_preimage_isOpen, preimage_setOf_eq]
      ¬∑ simpa [‚Üê mem_preimage, setOf_mem_eq] using
          hs.preimage WithTop.continuous_coe |>.preimage <| map_continuous f
      ¬∑ intro x; simp +contextual

@[simp]
lemma WithTop.lift_coe {X} [TopologicalSpace X] {U : Set X} [DecidablePred (¬∑ ‚àà U)] (Uo : IsOpen U)
    (f : C(U, Œ±)) (x : U) :
    WithTop.lift Uo f (x : X) = (WithLowerSet.toLowerSet ‚àò WithTop.some) (f x) := by
  simp [WithTop.lift]

@[simp]
lemma WithTop.lift_of_mem {X} [TopologicalSpace X] {U : Set X} [DecidablePred (¬∑ ‚àà U)]
    (Uo : IsOpen U) (f : C(U, Œ±)) {x : X} (hx : x ‚àà U) :
    WithTop.lift Uo f x = (WithLowerSet.toLowerSet ‚àò WithTop.some) (f ‚ü®x, hx‚ü©) := by
  simp [WithTop.lift, hx]

@[simp]
lemma WithTop.lift_of_notMem {X} [TopologicalSpace X] {U : Set X} [DecidablePred (¬∑ ‚àà U)]
    (Uo : IsOpen U) (f : C(U, Œ±)) {x : X} (hx : x ‚àâ U) : WithTop.lift Uo f x = ‚ä§ := by
  simp [WithTop.lift, hx]

@[simp]
lemma WithTop.lift_restrict {X} [TopologicalSpace X] {U : Set X} [DecidablePred (¬∑ ‚àà U)]
    (Uo : IsOpen U) (f : C(U, Œ±)) :
    (WithTop.lift Uo f).restrict U =
      .comp ‚ü®WithLowerSet.toLowerSet ‚àò WithTop.some, continuous_coe‚ü© f := by
  ext x; simp [WithTop.lift]

@[simp]
lemma WithTop.lift_restrict_compl {X} [TopologicalSpace X] {U : Set X} [DecidablePred (¬∑ ‚àà U)]
    (Uo : IsOpen U) (f : C(U, Œ±)) :
    (WithTop.lift Uo f).restrict U·∂ú = .const _ ‚ä§ := by
  ext x; simpa [WithTop.lift, -Subtype.coe_prop] using x.2

end IsLowerSet
end Topology
