/-
Copyright (c) 2023 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Topology.Order.Lattice
import Mathlib.Order.PartialSups

/-!
# Continuity of `partialSups`

In this file we prove that `partialSups` of a sequence of continuous functions is continuous
as well as versions for `Filter.Tendsto`, `ContinuousAt`, `ContinuousWithinAt`, and `ContinuousOn`.
-/

open Filter
open scoped Topology

variable {L : Type*} [SemilatticeSup L] [TopologicalSpace L] [ContinuousSup L]

namespace Filter.Tendsto

variable {Î± : Type*} {l : Filter Î±} {f : â„• â†’ Î± â†’ L} {g : â„• â†’ L} {n : â„•}

protected lemma partialSups (hf : âˆ€ k â‰¤ n, Tendsto (f k) l (ğ“ (g k))) :
    Tendsto (partialSups f n) l (ğ“ (partialSups g n)) := by
  simp only [partialSups_eq_sup'_range]
  refine finset_sup'_nhds _ ?_
  simpa [Nat.lt_succ_iff]

protected lemma partialSups_apply (hf : âˆ€ k â‰¤ n, Tendsto (f k) l (ğ“ (g k))) :
    Tendsto (fun a â†¦ partialSups (f Â· a) n) l (ğ“ (partialSups g n)) := by
  simpa only [â† Pi.partialSups_apply] using Tendsto.partialSups hf

end Filter.Tendsto

variable {X : Type*} [TopologicalSpace X] {f : â„• â†’ X â†’ L} {n : â„•} {s : Set X} {x : X}

protected lemma ContinuousAt.partialSups_apply (hf : âˆ€ k â‰¤ n, ContinuousAt (f k) x) :
    ContinuousAt (fun a â†¦ partialSups (f Â· a) n) x :=
  Tendsto.partialSups_apply hf

protected lemma ContinuousAt.partialSups (hf : âˆ€ k â‰¤ n, ContinuousAt (f k) x) :
    ContinuousAt (partialSups f n) x := by
  simpa only [â† Pi.partialSups_apply] using ContinuousAt.partialSups_apply hf

protected lemma ContinuousWithinAt.partialSups_apply (hf : âˆ€ k â‰¤ n, ContinuousWithinAt (f k) s x) :
    ContinuousWithinAt (fun a â†¦ partialSups (f Â· a) n) s x :=
  Tendsto.partialSups_apply hf

protected lemma ContinuousWithinAt.partialSups (hf : âˆ€ k â‰¤ n, ContinuousWithinAt (f k) s x) :
    ContinuousWithinAt (partialSups f n) s x := by
  simpa only [â† Pi.partialSups_apply] using ContinuousWithinAt.partialSups_apply hf

protected lemma ContinuousOn.partialSups_apply (hf : âˆ€ k â‰¤ n, ContinuousOn (f k) s) :
    ContinuousOn (fun a â†¦ partialSups (f Â· a) n) s := fun x hx â†¦
  ContinuousWithinAt.partialSups_apply fun k hk â†¦ hf k hk x hx

protected lemma ContinuousOn.partialSups (hf : âˆ€ k â‰¤ n, ContinuousOn (f k) s) :
    ContinuousOn (partialSups f n) s := fun x hx â†¦
  ContinuousWithinAt.partialSups fun k hk â†¦ hf k hk x hx

protected lemma Continuous.partialSups_apply (hf : âˆ€ k â‰¤ n, Continuous (f k)) :
    Continuous (fun a â†¦ partialSups (f Â· a) n) :=
  continuous_iff_continuousAt.2 fun _ â†¦ ContinuousAt.partialSups_apply fun k hk â†¦
    (hf k hk).continuousAt

protected lemma Continuous.partialSups (hf : âˆ€ k â‰¤ n, Continuous (f k)) :
    Continuous (partialSups f n) :=
  continuous_iff_continuousAt.2 fun _ â†¦ ContinuousAt.partialSups fun k hk â†¦ (hf k hk).continuousAt
