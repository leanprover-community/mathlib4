/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Yury Kudryashov
-/
import Mathlib.Topology.Order.IsLUB

/-!
# Order topology on a densely ordered set
-/

open Set Filter TopologicalSpace Topology Function

open OrderDual (toDual ofDual)

variable {Î± Î² : Type*}

section DenselyOrdered

variable [TopologicalSpace Î±] [LinearOrder Î±] [OrderTopology Î±] [DenselyOrdered Î±] {a b : Î±}
  {s : Set Î±}

/-- The closure of the interval `(a, +âˆ)` is the closed interval `[a, +âˆ)`, unless `a` is a top
element. -/
theorem closure_Ioi' {a : Î±} (h : (Ioi a).Nonempty) : closure (Ioi a) = Ici a := by
  apply Subset.antisymm
  Â· exact closure_minimal Ioi_subset_Ici_self isClosed_Ici
  Â· rw [â† diff_subset_closure_iff, Ici_diff_Ioi_same, singleton_subset_iff]
    exact isGLB_Ioi.mem_closure h

/-- The closure of the interval `(a, +âˆ)` is the closed interval `[a, +âˆ)`. -/
@[simp]
theorem closure_Ioi (a : Î±) [NoMaxOrder Î±] : closure (Ioi a) = Ici a :=
  closure_Ioi' nonempty_Ioi

/-- The closure of the interval `(-âˆ, a)` is the closed interval `(-âˆ, a]`, unless `a` is a bottom
element. -/
theorem closure_Iio' (h : (Iio a).Nonempty) : closure (Iio a) = Iic a :=
  closure_Ioi' (Î± := Î±áµ’áµˆ) h

/-- The closure of the interval `(-âˆ, a)` is the interval `(-âˆ, a]`. -/
@[simp]
theorem closure_Iio (a : Î±) [NoMinOrder Î±] : closure (Iio a) = Iic a :=
  closure_Iio' nonempty_Iio

/-- The closure of the open interval `(a, b)` is the closed interval `[a, b]`. -/
@[simp]
theorem closure_Ioo {a b : Î±} (hab : a â‰  b) : closure (Ioo a b) = Icc a b := by
  apply Subset.antisymm
  Â· exact closure_minimal Ioo_subset_Icc_self isClosed_Icc
  Â· rcases hab.lt_or_gt with hab | hab
    Â· rw [â† diff_subset_closure_iff, Icc_diff_Ioo_same hab.le]
      have hab' : (Ioo a b).Nonempty := nonempty_Ioo.2 hab
      simp only [insert_subset_iff, singleton_subset_iff]
      exact âŸ¨(isGLB_Ioo hab).mem_closure hab', (isLUB_Ioo hab).mem_closure hab'âŸ©
    Â· rw [Icc_eq_empty_of_lt hab]
      exact empty_subset _

/-- The closure of the interval `(a, b]` is the closed interval `[a, b]`. -/
@[simp]
theorem closure_Ioc {a b : Î±} (hab : a â‰  b) : closure (Ioc a b) = Icc a b := by
  apply Subset.antisymm
  Â· exact closure_minimal Ioc_subset_Icc_self isClosed_Icc
  Â· apply Subset.trans _ (closure_mono Ioo_subset_Ioc_self)
    rw [closure_Ioo hab]

/-- The closure of the interval `[a, b)` is the closed interval `[a, b]`. -/
@[simp]
theorem closure_Ico {a b : Î±} (hab : a â‰  b) : closure (Ico a b) = Icc a b := by
  apply Subset.antisymm
  Â· exact closure_minimal Ico_subset_Icc_self isClosed_Icc
  Â· apply Subset.trans _ (closure_mono Ioo_subset_Ico_self)
    rw [closure_Ioo hab]

@[simp]
theorem interior_Ici' {a : Î±} (ha : (Iio a).Nonempty) : interior (Ici a) = Ioi a := by
  rw [â† compl_Iio, interior_compl, closure_Iio' ha, compl_Iic]

theorem interior_Ici [NoMinOrder Î±] {a : Î±} : interior (Ici a) = Ioi a :=
  interior_Ici' nonempty_Iio

@[simp]
theorem interior_Iic' {a : Î±} (ha : (Ioi a).Nonempty) : interior (Iic a) = Iio a :=
  interior_Ici' (Î± := Î±áµ’áµˆ) ha

theorem interior_Iic [NoMaxOrder Î±] {a : Î±} : interior (Iic a) = Iio a :=
  interior_Iic' nonempty_Ioi

@[simp]
theorem interior_Icc [NoMinOrder Î±] [NoMaxOrder Î±] {a b : Î±} : interior (Icc a b) = Ioo a b := by
  rw [â† Ici_inter_Iic, interior_inter, interior_Ici, interior_Iic, Ioi_inter_Iio]

@[simp]
theorem Icc_mem_nhds_iff [NoMinOrder Î±] [NoMaxOrder Î±] {a b x : Î±} :
    Icc a b âˆˆ ğ“ x â†” x âˆˆ Ioo a b := by
  rw [â† interior_Icc, mem_interior_iff_mem_nhds]

@[simp]
theorem interior_Ico [NoMinOrder Î±] {a b : Î±} : interior (Ico a b) = Ioo a b := by
  rw [â† Ici_inter_Iio, interior_inter, interior_Ici, interior_Iio, Ioi_inter_Iio]

@[simp]
theorem Ico_mem_nhds_iff [NoMinOrder Î±] {a b x : Î±} : Ico a b âˆˆ ğ“ x â†” x âˆˆ Ioo a b := by
  rw [â† interior_Ico, mem_interior_iff_mem_nhds]

@[simp]
theorem interior_Ioc [NoMaxOrder Î±] {a b : Î±} : interior (Ioc a b) = Ioo a b := by
  rw [â† Ioi_inter_Iic, interior_inter, interior_Ioi, interior_Iic, Ioi_inter_Iio]

@[simp]
theorem Ioc_mem_nhds_iff [NoMaxOrder Î±] {a b x : Î±} : Ioc a b âˆˆ ğ“ x â†” x âˆˆ Ioo a b := by
  rw [â† interior_Ioc, mem_interior_iff_mem_nhds]

theorem closure_interior_Icc {a b : Î±} (h : a â‰  b) : closure (interior (Icc a b)) = Icc a b :=
  (closure_minimal interior_subset isClosed_Icc).antisymm <|
    calc
      Icc a b = closure (Ioo a b) := (closure_Ioo h).symm
      _ âŠ† closure (interior (Icc a b)) :=
        closure_mono (interior_maximal Ioo_subset_Icc_self isOpen_Ioo)

theorem Ioc_subset_closure_interior (a b : Î±) : Ioc a b âŠ† closure (interior (Ioc a b)) := by
  rcases eq_or_ne a b with (rfl | h)
  Â· simp
  Â· calc
      Ioc a b âŠ† Icc a b := Ioc_subset_Icc_self
      _ = closure (Ioo a b) := (closure_Ioo h).symm
      _ âŠ† closure (interior (Ioc a b)) :=
        closure_mono (interior_maximal Ioo_subset_Ioc_self isOpen_Ioo)

theorem Ico_subset_closure_interior (a b : Î±) : Ico a b âŠ† closure (interior (Ico a b)) := by
  simpa only [Ioc_toDual] using
    Ioc_subset_closure_interior (OrderDual.toDual b) (OrderDual.toDual a)

@[simp]
theorem frontier_Ici' {a : Î±} (ha : (Iio a).Nonempty) : frontier (Ici a) = {a} := by
  simp [frontier, ha]

theorem frontier_Ici [NoMinOrder Î±] {a : Î±} : frontier (Ici a) = {a} :=
  frontier_Ici' nonempty_Iio

@[simp]
theorem frontier_Iic' {a : Î±} (ha : (Ioi a).Nonempty) : frontier (Iic a) = {a} := by
  simp [frontier, ha]

theorem frontier_Iic [NoMaxOrder Î±] {a : Î±} : frontier (Iic a) = {a} :=
  frontier_Iic' nonempty_Ioi

@[simp]
theorem frontier_Ioi' {a : Î±} (ha : (Ioi a).Nonempty) : frontier (Ioi a) = {a} := by
  simp [frontier, closure_Ioi' ha]

theorem frontier_Ioi [NoMaxOrder Î±] {a : Î±} : frontier (Ioi a) = {a} :=
  frontier_Ioi' nonempty_Ioi

@[simp]
theorem frontier_Iio' {a : Î±} (ha : (Iio a).Nonempty) : frontier (Iio a) = {a} := by
  simp [frontier, closure_Iio' ha]

theorem frontier_Iio [NoMinOrder Î±] {a : Î±} : frontier (Iio a) = {a} :=
  frontier_Iio' nonempty_Iio

@[simp]
theorem frontier_Icc [NoMinOrder Î±] [NoMaxOrder Î±] {a b : Î±} (h : a â‰¤ b) :
    frontier (Icc a b) = {a, b} := by simp [frontier, h, Icc_diff_Ioo_same]

@[simp]
theorem frontier_Ioo {a b : Î±} (h : a < b) : frontier (Ioo a b) = {a, b} := by
  rw [frontier, closure_Ioo h.ne, interior_Ioo, Icc_diff_Ioo_same h.le]

@[simp]
theorem frontier_Ico [NoMinOrder Î±] {a b : Î±} (h : a < b) : frontier (Ico a b) = {a, b} := by
  rw [frontier, closure_Ico h.ne, interior_Ico, Icc_diff_Ioo_same h.le]

@[simp]
theorem frontier_Ioc [NoMaxOrder Î±] {a b : Î±} (h : a < b) : frontier (Ioc a b) = {a, b} := by
  rw [frontier, closure_Ioc h.ne, interior_Ioc, Icc_diff_Ioo_same h.le]

theorem nhdsWithin_Ioi_neBot' {a b : Î±} (Hâ‚ : (Ioi a).Nonempty) (Hâ‚‚ : a â‰¤ b) :
    NeBot (ğ“[Ioi a] b) :=
  mem_closure_iff_nhdsWithin_neBot.1 <| by rwa [closure_Ioi' Hâ‚]

theorem nhdsWithin_Ioi_neBot [NoMaxOrder Î±] {a b : Î±} (H : a â‰¤ b) : NeBot (ğ“[Ioi a] b) :=
  nhdsWithin_Ioi_neBot' nonempty_Ioi H

theorem nhdsGT_neBot_of_exists_gt {a : Î±} (H : âˆƒ b, a < b) : NeBot (ğ“[>] a) :=
  nhdsWithin_Ioi_neBot' H (le_refl a)

instance nhdsGT_neBot [NoMaxOrder Î±] (a : Î±) : NeBot (ğ“[>] a) := nhdsWithin_Ioi_neBot le_rfl

theorem nhdsWithin_Iio_neBot' {b c : Î±} (Hâ‚ : (Iio c).Nonempty) (Hâ‚‚ : b â‰¤ c) :
    NeBot (ğ“[Iio c] b) :=
  mem_closure_iff_nhdsWithin_neBot.1 <| by rwa [closure_Iio' Hâ‚]

theorem nhdsWithin_Iio_neBot [NoMinOrder Î±] {a b : Î±} (H : a â‰¤ b) : NeBot (ğ“[Iio b] a) :=
  nhdsWithin_Iio_neBot' nonempty_Iio H

theorem nhdsWithin_Iio_self_neBot' {b : Î±} (H : (Iio b).Nonempty) : NeBot (ğ“[<] b) :=
  nhdsWithin_Iio_neBot' H (le_refl b)

instance nhdsLT_neBot [NoMinOrder Î±] (a : Î±) : NeBot (ğ“[<] a) := nhdsWithin_Iio_neBot (le_refl a)

theorem right_nhdsWithin_Ico_neBot {a b : Î±} (H : a < b) : NeBot (ğ“[Ico a b] b) :=
  (isLUB_Ico H).nhdsWithin_neBot (nonempty_Ico.2 H)

theorem left_nhdsWithin_Ioc_neBot {a b : Î±} (H : a < b) : NeBot (ğ“[Ioc a b] a) :=
  (isGLB_Ioc H).nhdsWithin_neBot (nonempty_Ioc.2 H)

theorem left_nhdsWithin_Ioo_neBot {a b : Î±} (H : a < b) : NeBot (ğ“[Ioo a b] a) :=
  (isGLB_Ioo H).nhdsWithin_neBot (nonempty_Ioo.2 H)

theorem right_nhdsWithin_Ioo_neBot {a b : Î±} (H : a < b) : NeBot (ğ“[Ioo a b] b) :=
  (isLUB_Ioo H).nhdsWithin_neBot (nonempty_Ioo.2 H)

theorem comap_coe_nhdsLT_of_Ioo_subset (hb : s âŠ† Iio b) (hs : s.Nonempty â†’ âˆƒ a < b, Ioo a b âŠ† s) :
    comap ((â†‘) : s â†’ Î±) (ğ“[<] b) = atTop := by
  nontriviality
  haveI : Nonempty s := nontrivial_iff_nonempty.1 â€¹_â€º
  rcases hs (nonempty_subtype.1 â€¹_â€º) with âŸ¨a, h, hsâŸ©
  ext u; constructor
  Â· rintro âŸ¨t, ht, htsâŸ©
    obtain âŸ¨x, âŸ¨hxa : a â‰¤ x, hxb : x < bâŸ©, hxt : Ioo x b âŠ† tâŸ© :=
      (mem_nhdsLT_iff_exists_mem_Ico_Ioo_subset h).mp ht
    obtain âŸ¨y, hxy, hybâŸ© := exists_between hxb
    refine mem_of_superset (mem_atTop âŸ¨y, hs âŸ¨hxa.trans_lt hxy, hybâŸ©âŸ©) ?_
    rintro âŸ¨z, hzsâŸ© (hyz : y â‰¤ z)
    exact hts (hxt âŸ¨hxy.trans_le hyz, hb hzsâŸ©)
  Â· intro hu
    obtain âŸ¨x : s, hx : âˆ€ z, x â‰¤ z â†’ z âˆˆ uâŸ© := mem_atTop_sets.1 hu
    exact âŸ¨Ioo x b, Ioo_mem_nhdsLT (hb x.2), fun z hz => hx _ hz.1.leâŸ©

theorem comap_coe_nhdsGT_of_Ioo_subset (ha : s âŠ† Ioi a) (hs : s.Nonempty â†’ âˆƒ b > a, Ioo a b âŠ† s) :
    comap ((â†‘) : s â†’ Î±) (ğ“[>] a) = atBot := by
  apply comap_coe_nhdsLT_of_Ioo_subset (show ofDual â»Â¹' s âŠ† Iio (toDual a) from ha)
  simp only [OrderDual.exists, Ioo_toDual]
  exact hs

theorem map_coe_atTop_of_Ioo_subset (hb : s âŠ† Iio b) (hs : âˆ€ a' < b, âˆƒ a < b, Ioo a b âŠ† s) :
    map ((â†‘) : s â†’ Î±) atTop = ğ“[<] b := by
  rcases eq_empty_or_nonempty (Iio b) with (hb' | âŸ¨a, haâŸ©)
  Â· have : IsEmpty s := âŸ¨fun x => hb'.subset (hb x.2)âŸ©
    rw [filter_eq_bot_of_isEmpty atTop, Filter.map_bot, hb', nhdsWithin_empty]
  Â· rw [â† comap_coe_nhdsLT_of_Ioo_subset hb fun _ => hs a ha, map_comap_of_mem]
    rw [Subtype.range_val]
    exact (mem_nhdsLT_iff_exists_Ioo_subset' ha).2 (hs a ha)

theorem map_coe_atBot_of_Ioo_subset (ha : s âŠ† Ioi a) (hs : âˆ€ b' > a, âˆƒ b > a, Ioo a b âŠ† s) :
    map ((â†‘) : s â†’ Î±) atBot = ğ“[>] a := by
  -- the elaborator gets stuck without `(... :)`
  refine (map_coe_atTop_of_Ioo_subset (show ofDual â»Â¹' s âŠ† Iio (toDual a) from ha)
    fun b' hb' => ?_ :)
  simpa using hs b' hb'

/-- The `atTop` filter for an open interval `Ioo a b` comes from the left-neighbourhoods filter at
the right endpoint in the ambient order. -/
theorem comap_coe_Ioo_nhdsLT (a b : Î±) : comap ((â†‘) : Ioo a b â†’ Î±) (ğ“[<] b) = atTop :=
  comap_coe_nhdsLT_of_Ioo_subset Ioo_subset_Iio_self fun h => âŸ¨a, nonempty_Ioo.1 h, Subset.refl _âŸ©

/-- The `atBot` filter for an open interval `Ioo a b` comes from the right-neighbourhoods filter at
the left endpoint in the ambient order. -/
theorem comap_coe_Ioo_nhdsGT (a b : Î±) : comap ((â†‘) : Ioo a b â†’ Î±) (ğ“[>] a) = atBot :=
  comap_coe_nhdsGT_of_Ioo_subset Ioo_subset_Ioi_self fun h => âŸ¨b, nonempty_Ioo.1 h, Subset.refl _âŸ©

theorem comap_coe_Ioi_nhdsGT (a : Î±) : comap ((â†‘) : Ioi a â†’ Î±) (ğ“[>] a) = atBot :=
  comap_coe_nhdsGT_of_Ioo_subset (Subset.refl _) fun âŸ¨x, hxâŸ© => âŸ¨x, hx, Ioo_subset_Ioi_selfâŸ©

theorem comap_coe_Iio_nhdsLT (a : Î±) : comap ((â†‘) : Iio a â†’ Î±) (ğ“[<] a) = atTop :=
  comap_coe_Ioi_nhdsGT (Î± := Î±áµ’áµˆ) a

@[simp]
theorem map_coe_Ioo_atTop {a b : Î±} (h : a < b) : map ((â†‘) : Ioo a b â†’ Î±) atTop = ğ“[<] b :=
  map_coe_atTop_of_Ioo_subset Ioo_subset_Iio_self fun _ _ => âŸ¨_, h, Subset.refl _âŸ©

@[simp]
theorem map_coe_Ioo_atBot {a b : Î±} (h : a < b) : map ((â†‘) : Ioo a b â†’ Î±) atBot = ğ“[>] a :=
  map_coe_atBot_of_Ioo_subset Ioo_subset_Ioi_self fun _ _ => âŸ¨_, h, Subset.refl _âŸ©

@[simp]
theorem map_coe_Ioi_atBot (a : Î±) : map ((â†‘) : Ioi a â†’ Î±) atBot = ğ“[>] a :=
  map_coe_atBot_of_Ioo_subset (Subset.refl _) fun b hb => âŸ¨b, hb, Ioo_subset_Ioi_selfâŸ©

@[simp]
theorem map_coe_Iio_atTop (a : Î±) : map ((â†‘) : Iio a â†’ Î±) atTop = ğ“[<] a :=
  map_coe_Ioi_atBot (Î± := Î±áµ’áµˆ) _

variable {l : Filter Î²} {f : Î± â†’ Î²}

@[simp]
theorem tendsto_comp_coe_Ioo_atTop (h : a < b) :
    Tendsto (fun x : Ioo a b => f x) atTop l â†” Tendsto f (ğ“[<] b) l := by
  rw [â† map_coe_Ioo_atTop h, tendsto_map'_iff]; rfl

@[simp]
theorem tendsto_comp_coe_Ioo_atBot (h : a < b) :
    Tendsto (fun x : Ioo a b => f x) atBot l â†” Tendsto f (ğ“[>] a) l := by
  rw [â† map_coe_Ioo_atBot h, tendsto_map'_iff]; rfl

@[simp]
theorem tendsto_comp_coe_Ioi_atBot :
    Tendsto (fun x : Ioi a => f x) atBot l â†” Tendsto f (ğ“[>] a) l := by
  rw [â† map_coe_Ioi_atBot, tendsto_map'_iff]; rfl

@[simp]
theorem tendsto_comp_coe_Iio_atTop :
    Tendsto (fun x : Iio a => f x) atTop l â†” Tendsto f (ğ“[<] a) l := by
  rw [â† map_coe_Iio_atTop, tendsto_map'_iff]; rfl

@[simp]
theorem tendsto_Ioo_atTop {f : Î² â†’ Ioo a b} :
    Tendsto f l atTop â†” Tendsto (fun x => (f x : Î±)) l (ğ“[<] b) := by
  rw [â† comap_coe_Ioo_nhdsLT, tendsto_comap_iff]; rfl

@[simp]
theorem tendsto_Ioo_atBot {f : Î² â†’ Ioo a b} :
    Tendsto f l atBot â†” Tendsto (fun x => (f x : Î±)) l (ğ“[>] a) := by
  rw [â† comap_coe_Ioo_nhdsGT, tendsto_comap_iff]; rfl

@[simp]
theorem tendsto_Ioi_atBot {f : Î² â†’ Ioi a} :
    Tendsto f l atBot â†” Tendsto (fun x => (f x : Î±)) l (ğ“[>] a) := by
  rw [â† comap_coe_Ioi_nhdsGT, tendsto_comap_iff]; rfl

@[simp]
theorem tendsto_Iio_atTop {f : Î² â†’ Iio a} :
    Tendsto f l atTop â†” Tendsto (fun x => (f x : Î±)) l (ğ“[<] a) := by
  rw [â† comap_coe_Iio_nhdsLT, tendsto_comap_iff]; rfl

instance (x : Î±) [Nontrivial Î±] : NeBot (ğ“[â‰ ] x) := by
  refine forall_mem_nonempty_iff_neBot.1 fun s hs => ?_
  obtain âŸ¨u, u_open, xu, usâŸ© : âˆƒ u : Set Î±, IsOpen u âˆ§ x âˆˆ u âˆ§ u âˆ© {x}á¶œ âŠ† s := mem_nhdsWithin.1 hs
  obtain âŸ¨a, b, a_lt_b, habâŸ© : âˆƒ a b : Î±, a < b âˆ§ Ioo a b âŠ† u := u_open.exists_Ioo_subset âŸ¨x, xuâŸ©
  obtain âŸ¨y, hyâŸ© : âˆƒ y, a < y âˆ§ y < b := exists_between a_lt_b
  rcases ne_or_eq x y with (xy | rfl)
  Â· exact âŸ¨y, us âŸ¨hab hy, xy.symmâŸ©âŸ©
  obtain âŸ¨z, hzâŸ© : âˆƒ z, a < z âˆ§ z < x := exists_between hy.1
  exact âŸ¨z, us âŸ¨hab âŸ¨hz.1, hz.2.trans hy.2âŸ©, hz.2.neâŸ©âŸ©

/-- If the order topology for a dense linear ordering is discrete, the space has at most one point.

We would prefer for this to be an instance but even at `(priority := 100)` this was problematic so
we have deferred this issue. TODO Promote this to an `instance`! -/
lemma DenselyOrdered.subsingleton_of_discreteTopology [DiscreteTopology Î±] : Subsingleton Î± := by
  suffices âˆ€ a b : Î±, b â‰¤ a from âŸ¨fun a b â†¦ le_antisymm (this b a) (this a b)âŸ©
  intro a b
  by_contra! contra
  suffices b âˆˆ Ioo a b by
    #adaptation_note /-- 2025-10-25 https://github.com/leanprover/lean4/issues/10953
      Used to be `grind`. -/
    simp_all
  rw [â† (isClosed_discrete (Ioo a b)).closure_eq, closure_Ioo contra.ne]
  grind

/-- Let `s` be a dense set in a nontrivial dense linear order `Î±`. If `s` is a
separable space (e.g., if `Î±` has a second countable topology), then there exists a countable
dense subset `t âŠ† s` such that `t` does not contain bottom/top elements of `Î±`. -/
theorem Dense.exists_countable_dense_subset_no_bot_top [Nontrivial Î±] {s : Set Î±} [SeparableSpace s]
    (hs : Dense s) :
    âˆƒ t, t âŠ† s âˆ§ t.Countable âˆ§ Dense t âˆ§ (âˆ€ x, IsBot x â†’ x âˆ‰ t) âˆ§ âˆ€ x, IsTop x â†’ x âˆ‰ t := by
  rcases hs.exists_countable_dense_subset with âŸ¨t, hts, htc, htdâŸ©
  refine âŸ¨t \ ({ x | IsBot x } âˆª { x | IsTop x }), ?_, ?_, ?_, fun x hx => ?_, fun x hx => ?_âŸ©
  Â· exact diff_subset.trans hts
  Â· exact htc.mono diff_subset
  Â· exact htd.diff_finite ((subsingleton_isBot Î±).finite.union (subsingleton_isTop Î±).finite)
  Â· simp [hx]
  Â· simp [hx]

variable (Î±) in
/-- If `Î±` is a nontrivial separable dense linear order, then there exists a
countable dense set `s : Set Î±` that contains neither top nor bottom elements of `Î±`.
For a dense set containing both bot and top elements, see
`exists_countable_dense_bot_top`. -/
theorem exists_countable_dense_no_bot_top [SeparableSpace Î±] [Nontrivial Î±] :
    âˆƒ s : Set Î±, s.Countable âˆ§ Dense s âˆ§ (âˆ€ x, IsBot x â†’ x âˆ‰ s) âˆ§ âˆ€ x, IsTop x â†’ x âˆ‰ s := by
  simpa using dense_univ.exists_countable_dense_subset_no_bot_top

/-- `Set.Ico a b` is only closed if it is empty. -/
@[simp]
theorem isClosed_Ico_iff {a b : Î±} : IsClosed (Set.Ico a b) â†” b â‰¤ a := by
  refine âŸ¨fun h => le_of_not_gt fun hab => ?_, by simp_allâŸ©
  have := h.closure_eq
  rw [closure_Ico hab.ne, Icc_eq_Ico_same_iff] at this
  exact this hab.le

/-- `Set.Ioc a b` is only closed if it is empty. -/
@[simp]
theorem isClosed_Ioc_iff {a b : Î±} : IsClosed (Set.Ioc a b) â†” b â‰¤ a := by
  refine âŸ¨fun h => le_of_not_gt fun hab => ?_, by simp_allâŸ©
  have := h.closure_eq
  rw [closure_Ioc hab.ne, Icc_eq_Ioc_same_iff] at this
  exact this hab.le

/-- `Set.Ioo a b` is only closed if it is empty. -/
@[simp]
theorem isClosed_Ioo_iff {a b : Î±} : IsClosed (Set.Ioo a b) â†” b â‰¤ a := by
  refine âŸ¨fun h => le_of_not_gt fun hab => ?_, by simp_allâŸ©
  have := h.closure_eq
  rw [closure_Ioo hab.ne, Icc_eq_Ioo_same_iff] at this
  exact this hab.le

end DenselyOrdered
