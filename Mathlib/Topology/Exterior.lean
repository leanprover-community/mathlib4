/-
Copyright (c) 2023 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Yury Kudryashov
-/
import Mathlib.Topology.NhdsSet
import Mathlib.Topology.Inseparable

/-!
# Exterior of a set

We define `exterior s` to be the intersection of all neighborhoods of `s`,
see `Mathlib/Topology/Defs/Filter.lean`.
Note that this construction has no standard name in the literature.

In this file we prove basic properties of this operation.
-/

open Set Filter
open scoped Topology

variable {Î¹ : Sort*} {X : Type*} [TopologicalSpace X] {s t : Set X} {x y : X}

lemma exterior_singleton_eq_ker_nhds (x : X) : exterior {x} = (ğ“ x).ker := by simp [exterior]

@[simp]
theorem mem_exterior_singleton : x âˆˆ exterior {y} â†” x â¤³ y := by
  rw [exterior_singleton_eq_ker_nhds, ker_nhds_eq_specializes, mem_setOf]

lemma exterior_def (s : Set X) : exterior s = â‹‚â‚€ {t : Set X | IsOpen t âˆ§ s âŠ† t} :=
  (hasBasis_nhdsSet _).ker.trans sInter_eq_biInter.symm

lemma mem_exterior : x âˆˆ exterior s â†” âˆ€ U, IsOpen U â†’ s âŠ† U â†’ x âˆˆ U := by simp [exterior_def]

lemma subset_exterior_iff : s âŠ† exterior t â†” âˆ€ U, IsOpen U â†’ t âŠ† U â†’ s âŠ† U := by
  simp [exterior_def]

lemma subset_exterior : s âŠ† exterior s := subset_exterior_iff.2 fun _ _ â†¦ id

lemma exterior_minimal (hâ‚ : s âŠ† t) (hâ‚‚ : IsOpen t) : exterior s âŠ† t := by
  rw [exterior_def]; exact sInter_subset_of_mem âŸ¨hâ‚‚, hâ‚âŸ©

lemma IsOpen.exterior_eq (h : IsOpen s) : exterior s = s :=
  (exterior_minimal Subset.rfl h).antisymm subset_exterior

lemma IsOpen.exterior_subset (ht : IsOpen t) : exterior s âŠ† t â†” s âŠ† t :=
  âŸ¨subset_exterior.trans, fun h â†¦ exterior_minimal h htâŸ©

@[simp]
theorem exterior_iUnion (s : Î¹ â†’ Set X) : exterior (â‹ƒ i, s i) = â‹ƒ i, exterior (s i) := by
  simp only [exterior, nhdsSet_iUnion, ker_iSup]

@[simp]
theorem exterior_union (s t : Set X) : exterior (s âˆª t) = exterior s âˆª exterior t := by
  simp only [exterior, nhdsSet_union, ker_sup]

@[simp]
theorem exterior_sUnion (S : Set (Set X)) : exterior (â‹ƒâ‚€ S) = â‹ƒ s âˆˆ S, exterior s := by
  simp only [sUnion_eq_biUnion, exterior_iUnion]

theorem mem_exterior_iff_specializes : x âˆˆ exterior s â†” âˆƒ y âˆˆ s, x â¤³ y := calc
  x âˆˆ exterior s â†” x âˆˆ exterior (â‹ƒ y âˆˆ s, {y}) := by simp
  _ â†” âˆƒ y âˆˆ s, x â¤³ y := by
    simp only [exterior_iUnion, mem_exterior_singleton, mem_iUnionâ‚‚, exists_prop]

@[mono] lemma exterior_mono : Monotone (exterior : Set X â†’ Set X) :=
  fun _s _t h â†¦ ker_mono <| nhdsSet_mono h

/-- This name was used to be used for the `Iff` version,
see `exterior_subset_exterior_iff_nhdsSet`.
-/
@[gcongr] lemma exterior_subset_exterior (h : s âŠ† t) : exterior s âŠ† exterior t := exterior_mono h

@[simp] lemma exterior_subset_exterior_iff_nhdsSet : exterior s âŠ† exterior t â†” ğ“Ë¢ s â‰¤ ğ“Ë¢ t := by
  simp +contextual only [subset_exterior_iff, (hasBasis_nhdsSet _).ge_iff,
    and_imp, IsOpen.mem_nhdsSet, IsOpen.exterior_subset]

theorem exterior_eq_exterior_iff_nhdsSet : exterior s = exterior t â†” ğ“Ë¢ s = ğ“Ë¢ t := by
  simp [le_antisymm_iff]

lemma specializes_iff_exterior_subset : x â¤³ y â†” exterior {x} âŠ† exterior {y} := by
  simp [Specializes]

theorem exterior_iInter_subset {s : Î¹ â†’ Set X} : exterior (â‹‚ i, s i) âŠ† â‹‚ i, exterior (s i) :=
  exterior_mono.map_iInf_le

theorem exterior_inter_subset {s t : Set X} : exterior (s âˆ© t) âŠ† exterior s âˆ© exterior t :=
  exterior_mono.map_inf_le _ _

theorem exterior_sInter_subset {s : Set (Set X)} : exterior (â‹‚â‚€ s) âŠ† â‹‚ x âˆˆ s, exterior x :=
  exterior_mono.map_sInf_le

@[simp] lemma exterior_empty : exterior (âˆ… : Set X) = âˆ… := isOpen_empty.exterior_eq
@[simp] lemma exterior_univ : exterior (univ : Set X) = univ := isOpen_univ.exterior_eq

@[simp] lemma exterior_eq_empty : exterior s = âˆ… â†” s = âˆ… :=
  âŸ¨eq_bot_mono subset_exterior, by rintro rfl; exact exterior_emptyâŸ©

@[simp] lemma nhdsSet_exterior (s : Set X) : ğ“Ë¢ (exterior s) = ğ“Ë¢ s := by
  refine le_antisymm ((hasBasis_nhdsSet _).ge_iff.2 ?_) (nhdsSet_mono subset_exterior)
  exact fun U âŸ¨hUo, hsUâŸ© â†¦ hUo.mem_nhdsSet.2 <| hUo.exterior_subset.2 hsU

@[simp] lemma exterior_exterior (s : Set X) : exterior (exterior s) = exterior s := by
  simp only [exterior_eq_exterior_iff_nhdsSet, nhdsSet_exterior]
