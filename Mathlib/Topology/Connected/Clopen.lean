/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Yury Kudryashov
-/
import Mathlib.Data.Set.Subset
import Mathlib.Topology.Clopen
import Mathlib.Topology.Compactness.Compact
import Mathlib.Topology.Connected.Basic

/-!
# Connected subsets and their relation to clopen sets

In this file we show how connected subsets of a topological space are intimately connected
to clopen sets.

## Main declarations

+ `IsClopen.biUnion_connectedComponent_eq`: a clopen set is the union of its connected components.
+ `PreconnectedSpace.inductionâ‚‚`: an induction principle for preconnected spaces.
+ `ConnectedComponents`: The connected components of a topological space, as a quotient type.

-/

open Set Function Topology TopologicalSpace Relation

universe u v

variable {Î± : Type u} {Î² : Type v} {Î¹ : Type*} {X : Î¹ â†’ Type*} [TopologicalSpace Î±]
  {s t u v : Set Î±}

section Preconnected

/-- Preconnected sets are either contained in or disjoint to any given clopen set. -/
theorem IsPreconnected.subset_isClopen {s t : Set Î±} (hs : IsPreconnected s) (ht : IsClopen t)
    (hne : (s âˆ© t).Nonempty) : s âŠ† t :=
  hs.subset_left_of_subset_union ht.isOpen ht.compl.isOpen disjoint_compl_right (by simp) hne

theorem Sigma.isConnected_iff [âˆ€ i, TopologicalSpace (X i)] {s : Set (Î£ i, X i)} :
    IsConnected s â†” âˆƒ i t, IsConnected t âˆ§ s = Sigma.mk i '' t := by
  refine âŸ¨fun hs => ?_, ?_âŸ©
  Â· obtain âŸ¨âŸ¨i, xâŸ©, hxâŸ© := hs.nonempty
    have : s âŠ† range (Sigma.mk i) :=
      hs.isPreconnected.subset_isClopen isClopen_range_sigmaMk âŸ¨âŸ¨i, xâŸ©, hx, x, rflâŸ©
    exact âŸ¨i, Sigma.mk i â»Â¹' s, hs.preimage_of_isOpenMap sigma_mk_injective isOpenMap_sigmaMk this,
      (Set.image_preimage_eq_of_subset this).symmâŸ©
  Â· rintro âŸ¨i, t, ht, rflâŸ©
    exact ht.image _ continuous_sigmaMk.continuousOn

theorem Sigma.isPreconnected_iff [hÎ¹ : Nonempty Î¹] [âˆ€ i, TopologicalSpace (X i)]
    {s : Set (Î£ i, X i)} : IsPreconnected s â†” âˆƒ i t, IsPreconnected t âˆ§ s = Sigma.mk i '' t := by
  refine âŸ¨fun hs => ?_, ?_âŸ©
  Â· obtain rfl | h := s.eq_empty_or_nonempty
    Â· exact âŸ¨Classical.choice hÎ¹, âˆ…, isPreconnected_empty, (Set.image_empty _).symmâŸ©
    Â· obtain âŸ¨a, t, ht, rflâŸ© := Sigma.isConnected_iff.1 âŸ¨h, hsâŸ©
      exact âŸ¨a, t, ht.isPreconnected, rflâŸ©
  Â· rintro âŸ¨a, t, ht, rflâŸ©
    exact ht.image _ continuous_sigmaMk.continuousOn

theorem Sum.isConnected_iff [TopologicalSpace Î²] {s : Set (Î± âŠ• Î²)} :
    IsConnected s â†”
      (âˆƒ t, IsConnected t âˆ§ s = Sum.inl '' t) âˆ¨ âˆƒ t, IsConnected t âˆ§ s = Sum.inr '' t := by
  refine âŸ¨fun hs => ?_, ?_âŸ©
  Â· obtain âŸ¨x | x, hxâŸ© := hs.nonempty
    Â· have h : s âŠ† range Sum.inl :=
        hs.isPreconnected.subset_isClopen isClopen_range_inl âŸ¨.inl x, hx, x, rflâŸ©
      refine Or.inl âŸ¨Sum.inl â»Â¹' s, ?_, ?_âŸ©
      Â· exact hs.preimage_of_isOpenMap Sum.inl_injective isOpenMap_inl h
      Â· exact (image_preimage_eq_of_subset h).symm
    Â· have h : s âŠ† range Sum.inr :=
        hs.isPreconnected.subset_isClopen isClopen_range_inr âŸ¨.inr x, hx, x, rflâŸ©
      refine Or.inr âŸ¨Sum.inr â»Â¹' s, ?_, ?_âŸ©
      Â· exact hs.preimage_of_isOpenMap Sum.inr_injective isOpenMap_inr h
      Â· exact (image_preimage_eq_of_subset h).symm
  Â· rintro (âŸ¨t, ht, rflâŸ© | âŸ¨t, ht, rflâŸ©)
    Â· exact ht.image _ continuous_inl.continuousOn
    Â· exact ht.image _ continuous_inr.continuousOn

theorem Sum.isPreconnected_iff [TopologicalSpace Î²] {s : Set (Î± âŠ• Î²)} :
    IsPreconnected s â†”
      (âˆƒ t, IsPreconnected t âˆ§ s = Sum.inl '' t) âˆ¨ âˆƒ t, IsPreconnected t âˆ§ s = Sum.inr '' t := by
  refine âŸ¨fun hs => ?_, ?_âŸ©
  Â· obtain rfl | h := s.eq_empty_or_nonempty
    Â· exact Or.inl âŸ¨âˆ…, isPreconnected_empty, (Set.image_empty _).symmâŸ©
    obtain âŸ¨t, ht, rflâŸ© | âŸ¨t, ht, rflâŸ© := Sum.isConnected_iff.1 âŸ¨h, hsâŸ©
    Â· exact Or.inl âŸ¨t, ht.isPreconnected, rflâŸ©
    Â· exact Or.inr âŸ¨t, ht.isPreconnected, rflâŸ©
  Â· rintro (âŸ¨t, ht, rflâŸ© | âŸ¨t, ht, rflâŸ©)
    Â· exact ht.image _ continuous_inl.continuousOn
    Â· exact ht.image _ continuous_inr.continuousOn

/-- A continuous map from a connected space to a disjoint union `Î£ i, X i` can be lifted to one of
the components `X i`. See also `ContinuousMap.exists_lift_sigma` for a version with bundled
`ContinuousMap`s. -/
theorem Continuous.exists_lift_sigma [ConnectedSpace Î±] [âˆ€ i, TopologicalSpace (X i)]
    {f : Î± â†’ Î£ i, X i} (hf : Continuous f) :
    âˆƒ (i : Î¹) (g : Î± â†’ X i), Continuous g âˆ§ f = Sigma.mk i âˆ˜ g := by
  obtain âŸ¨i, hiâŸ© : âˆƒ i, range f âŠ† range (.mk i) := by
    rcases Sigma.isConnected_iff.1 (isConnected_range hf) with âŸ¨i, s, -, hsâŸ©
    exact âŸ¨i, hs.trans_subset (image_subset_range _ _)âŸ©
  rcases range_subset_range_iff_exists_comp.1 hi with âŸ¨g, rflâŸ©
  refine âŸ¨i, g, ?_, rflâŸ©
  rwa [â† IsEmbedding.sigmaMk.continuous_iff] at hf

theorem nonempty_inter [PreconnectedSpace Î±] {s t : Set Î±} :
    IsOpen s â†’ IsOpen t â†’ s âˆª t = univ â†’ s.Nonempty â†’ t.Nonempty â†’ (s âˆ© t).Nonempty := by
  simpa only [univ_inter, univ_subset_iff] using @PreconnectedSpace.isPreconnected_univ Î± _ _ s t

theorem isClopen_iff [PreconnectedSpace Î±] {s : Set Î±} : IsClopen s â†” s = âˆ… âˆ¨ s = univ :=
  âŸ¨fun hs =>
    by_contradiction fun h =>
      have h1 : s â‰  âˆ… âˆ§ sá¶œ â‰  âˆ… :=
        âŸ¨mt Or.inl h,
          mt (fun h2 => Or.inr <| (by rw [â† compl_compl s, h2, compl_empty] : s = univ)) hâŸ©
      let âŸ¨_, h2, h3âŸ© :=
        nonempty_inter hs.2 hs.1.isOpen_compl (union_compl_self s) (nonempty_iff_ne_empty.2 h1.1)
          (nonempty_iff_ne_empty.2 h1.2)
      h3 h2,
    by rintro (rfl | rfl) <;> [exact isClopen_empty; exact isClopen_univ]âŸ©

theorem IsClopen.eq_univ [PreconnectedSpace Î±] {s : Set Î±} (h' : IsClopen s) (h : s.Nonempty) :
    s = univ :=
  (isClopen_iff.mp h').resolve_left h.ne_empty

open Set.Notation in
lemma isClopen_preimage_val {X : Type*} [TopologicalSpace X] {u v : Set X}
    (hu : IsOpen u) (huv : Disjoint (frontier u) v) : IsClopen (v â†“âˆ© u) := by
  refine âŸ¨?_, isOpen_induced hu (f := Subtype.val)âŸ©
  refine isClosed_induced_iff.mpr âŸ¨closure u, isClosed_closure, ?_âŸ©
  apply image_val_injective
  simp only [Subtype.image_preimage_coe]
  rw [closure_eq_self_union_frontier, inter_union_distrib_left, inter_comm _ (frontier u),
    huv.inter_eq, union_empty]

section disjoint_subsets

variable [PreconnectedSpace Î±]
  {s : Î¹ â†’ Set Î±} (h_nonempty : âˆ€ i, (s i).Nonempty) (h_disj : Pairwise (Disjoint on s))
include h_nonempty h_disj

/-- In a preconnected space, any disjoint family of non-empty clopen subsets has at most one
element. -/
lemma subsingleton_of_disjoint_isClopen
    (h_clopen : âˆ€ i, IsClopen (s i)) :
    Subsingleton Î¹ := by
  replace h_nonempty : âˆ€ i, s i â‰  âˆ… := by intro i; rw [â† nonempty_iff_ne_empty]; exact h_nonempty i
  rw [â† not_nontrivial_iff_subsingleton]
  by_contra contra
  obtain âŸ¨i, j, h_neâŸ© := contra
  replace h_ne : s i âˆ© s j = âˆ… := by
    simpa only [â† bot_eq_empty, eq_bot_iff, â† inf_eq_inter, â† disjoint_iff_inf_le] using h_disj h_ne
  rcases isClopen_iff.mp (h_clopen i) with hi | hi
  Â· exact h_nonempty i hi
  Â· rw [hi, univ_inter] at h_ne
    exact h_nonempty j h_ne

/-- In a preconnected space, any disjoint cover by non-empty open subsets has at most one
element. -/
lemma subsingleton_of_disjoint_isOpen_iUnion_eq_univ
    (h_open : âˆ€ i, IsOpen (s i)) (h_Union : â‹ƒ i, s i = univ) :
    Subsingleton Î¹ := by
  refine subsingleton_of_disjoint_isClopen h_nonempty h_disj (fun i â†¦ âŸ¨?_, h_open iâŸ©)
  rw [â† isOpen_compl_iff, compl_eq_univ_diff, â† h_Union, iUnion_diff]
  refine isOpen_iUnion (fun j â†¦ ?_)
  rcases eq_or_ne i j with rfl | h_ne
  Â· simp
  Â· simpa only [(h_disj h_ne.symm).sdiff_eq_left] using h_open j

/-- In a preconnected space, any finite disjoint cover by non-empty closed subsets has at most one
element. -/
lemma subsingleton_of_disjoint_isClosed_iUnion_eq_univ [Finite Î¹]
    (h_closed : âˆ€ i, IsClosed (s i)) (h_Union : â‹ƒ i, s i = univ) :
    Subsingleton Î¹ := by
  refine subsingleton_of_disjoint_isClopen h_nonempty h_disj (fun i â†¦ âŸ¨h_closed i, ?_âŸ©)
  rw [â† isClosed_compl_iff, compl_eq_univ_diff, â† h_Union, iUnion_diff]
  refine isClosed_iUnion_of_finite (fun j â†¦ ?_)
  rcases eq_or_ne i j with rfl | h_ne
  Â· simp
  Â· simpa only [(h_disj h_ne.symm).sdiff_eq_left] using h_closed j

end disjoint_subsets

theorem frontier_eq_empty_iff [PreconnectedSpace Î±] {s : Set Î±} :
    frontier s = âˆ… â†” s = âˆ… âˆ¨ s = univ :=
  isClopen_iff_frontier_eq_empty.symm.trans isClopen_iff

theorem nonempty_frontier_iff [PreconnectedSpace Î±] {s : Set Î±} :
    (frontier s).Nonempty â†” s.Nonempty âˆ§ s â‰  univ := by
  simp only [nonempty_iff_ne_empty, Ne, frontier_eq_empty_iff, not_or]

/-- In a preconnected space, given a transitive relation `P`, if `P x y` and `P y x` are true
for `y` close enough to `x`, then `P x y` holds for all `x, y`. This is a version of the fact
that, if an equivalence relation has open classes, then it has a single equivalence class. -/
lemma PreconnectedSpace.inductionâ‚‚' [PreconnectedSpace Î±] (P : Î± â†’ Î± â†’ Prop)
    (h : âˆ€ x, âˆ€á¶  y in ğ“ x, P x y âˆ§ P y x) (h' : Transitive P) (x y : Î±) :
    P x y := by
  let u := {z | P x z}
  have A : IsClosed u := by
    apply isClosed_iff_nhds.2 (fun z hz â†¦ ?_)
    rcases hz _ (h z) with âŸ¨t, ht, h'tâŸ©
    exact h' h't ht.2
  have B : IsOpen u := by
    apply isOpen_iff_mem_nhds.2 (fun z hz â†¦ ?_)
    filter_upwards [h z] with t ht
    exact h' hz ht.1
  have C : u.Nonempty := âŸ¨x, (mem_of_mem_nhds (h x)).1âŸ©
  have D : u = Set.univ := IsClopen.eq_univ âŸ¨A, BâŸ© C
  change y âˆˆ u
  simp [D]

/-- In a preconnected space, if a symmetric transitive relation `P x y` is true for `y` close
enough to `x`, then it holds for all `x, y`. This is a version of the fact that, if an equivalence
relation has open classes, then it has a single equivalence class. -/
lemma PreconnectedSpace.inductionâ‚‚ [PreconnectedSpace Î±] (P : Î± â†’ Î± â†’ Prop)
    (h : âˆ€ x, âˆ€á¶  y in ğ“ x, P x y) (h' : Transitive P) (h'' : Symmetric P) (x y : Î±) :
    P x y := by
  refine PreconnectedSpace.inductionâ‚‚' P (fun z â†¦ ?_) h' x y
  filter_upwards [h z] with a ha
  exact âŸ¨ha, h'' haâŸ©

/-- In a preconnected set, given a transitive relation `P`, if `P x y` and `P y x` are true
for `y` close enough to `x`, then `P x y` holds for all `x, y`. This is a version of the fact
that, if an equivalence relation has open classes, then it has a single equivalence class. -/
lemma IsPreconnected.inductionâ‚‚' {s : Set Î±} (hs : IsPreconnected s) (P : Î± â†’ Î± â†’ Prop)
    (h : âˆ€ x âˆˆ s, âˆ€á¶  y in ğ“[s] x, P x y âˆ§ P y x)
    (h' : âˆ€ x y z, x âˆˆ s â†’ y âˆˆ s â†’ z âˆˆ s â†’ P x y â†’ P y z â†’ P x z)
    {x y : Î±} (hx : x âˆˆ s) (hy : y âˆˆ s) : P x y := by
  let Q : s â†’ s â†’ Prop := fun a b â†¦ P a b
  change Q âŸ¨x, hxâŸ© âŸ¨y, hyâŸ©
  have : PreconnectedSpace s := Subtype.preconnectedSpace hs
  apply PreconnectedSpace.inductionâ‚‚'
  Â· rintro âŸ¨x, hxâŸ©
    have Z := h x hx
    rwa [nhdsWithin_eq_map_subtype_coe] at Z
  Â· rintro âŸ¨a, haâŸ© âŸ¨b, hbâŸ© âŸ¨c, hcâŸ© hab hbc
    exact h' a b c ha hb hc hab hbc

/-- In a preconnected set, if a symmetric transitive relation `P x y` is true for `y` close
enough to `x`, then it holds for all `x, y`. This is a version of the fact that, if an equivalence
relation has open classes, then it has a single equivalence class. -/
lemma IsPreconnected.inductionâ‚‚ {s : Set Î±} (hs : IsPreconnected s) (P : Î± â†’ Î± â†’ Prop)
    (h : âˆ€ x âˆˆ s, âˆ€á¶  y in ğ“[s] x, P x y)
    (h' : âˆ€ x y z, x âˆˆ s â†’ y âˆˆ s â†’ z âˆˆ s â†’ P x y â†’ P y z â†’ P x z)
    (h'' : âˆ€ x y, x âˆˆ s â†’ y âˆˆ s â†’ P x y â†’ P y x)
    {x y : Î±} (hx : x âˆˆ s) (hy : y âˆˆ s) : P x y := by
  apply hs.inductionâ‚‚' P (fun z hz â†¦ ?_) h' hx hy
  filter_upwards [h z hz, self_mem_nhdsWithin] with a ha h'a
  exact âŸ¨ha, h'' z a hz h'a haâŸ©

/-- A set `s` is preconnected if and only if for every cover by two open sets that are disjoint on
`s`, it is contained in one of the two covering sets. -/
theorem isPreconnected_iff_subset_of_disjoint {s : Set Î±} :
    IsPreconnected s â†”
      âˆ€ u v, IsOpen u â†’ IsOpen v â†’ s âŠ† u âˆª v â†’ s âˆ© (u âˆ© v) = âˆ… â†’ s âŠ† u âˆ¨ s âŠ† v := by
  constructor <;> intro h
  Â· intro u v hu hv hs huv
    specialize h u v hu hv hs
    contrapose! huv
    simp only [not_subset] at huv
    rcases huv with âŸ¨âŸ¨x, hxs, hxuâŸ©, âŸ¨y, hys, hyvâŸ©âŸ©
    have hxv : x âˆˆ v := or_iff_not_imp_left.mp (hs hxs) hxu
    have hyu : y âˆˆ u := or_iff_not_imp_right.mp (hs hys) hyv
    exact h âŸ¨y, hys, hyuâŸ© âŸ¨x, hxs, hxvâŸ©
  Â· intro u v hu hv hs hsu hsv
    by_contra H
    specialize h u v hu hv hs (Set.not_nonempty_iff_eq_empty.mp H)
    apply H
    rcases h with h | h
    Â· rcases hsv with âŸ¨x, hxs, hxvâŸ©
      exact âŸ¨x, hxs, âŸ¨h hxs, hxvâŸ©âŸ©
    Â· rcases hsu with âŸ¨x, hxs, hxuâŸ©
      exact âŸ¨x, hxs, âŸ¨hxu, h hxsâŸ©âŸ©

/-- A set `s` is connected if and only if
for every cover by a finite collection of open sets that are pairwise disjoint on `s`,
it is contained in one of the members of the collection. -/
theorem isConnected_iff_sUnion_disjoint_open {s : Set Î±} :
    IsConnected s â†”
      âˆ€ U : Finset (Set Î±), (âˆ€ u v : Set Î±, u âˆˆ U â†’ v âˆˆ U â†’ (s âˆ© (u âˆ© v)).Nonempty â†’ u = v) â†’
        (âˆ€ u âˆˆ U, IsOpen u) â†’ (s âŠ† â‹ƒâ‚€ â†‘U) â†’ âˆƒ u âˆˆ U, s âŠ† u := by
  rw [IsConnected, isPreconnected_iff_subset_of_disjoint]
  classical
  refine âŸ¨fun âŸ¨hne, hâŸ© U hU hUo hsU => ?_, fun h => âŸ¨?_, fun u v hu hv hs hsuv => ?_âŸ©âŸ©
  Â· induction U using Finset.induction_on with
    | empty => exact absurd (by simpa using hsU) hne.not_subset_empty
    | insert u U uU IH =>
      simp only [â† forall_cond_comm, Finset.forall_mem_insert, Finset.exists_mem_insert,
        Finset.coe_insert, sUnion_insert, implies_true, true_and] at *
      refine (h _ hUo.1 (â‹ƒâ‚€ â†‘U) (isOpen_sUnion hUo.2) hsU ?_).imp_right ?_
      Â· refine subset_empty_iff.1 fun x âŸ¨hxs, hxu, v, hvU, hxvâŸ© => ?_
        exact ne_of_mem_of_not_mem hvU uU (hU.1 v hvU âŸ¨x, hxs, hxu, hxvâŸ©).symm
      Â· exact IH (fun u hu => (hU.2 u hu).2) hUo.2
  Â· simpa [subset_empty_iff, nonempty_iff_ne_empty] using h âˆ…
  Â· rw [â† not_nonempty_iff_eq_empty] at hsuv
    have := hsuv; rw [inter_comm u] at this
    simpa [*, or_imp, forall_and] using h {u, v}

/-- Preconnected sets are either contained in or disjoint to any given clopen set. -/
theorem disjoint_or_subset_of_isClopen {s t : Set Î±} (hs : IsPreconnected s) (ht : IsClopen t) :
    Disjoint s t âˆ¨ s âŠ† t :=
  (disjoint_or_nonempty_inter s t).imp_right <| hs.subset_isClopen ht

/-- A set `s` is preconnected if and only if
for every cover by two closed sets that are disjoint on `s`,
it is contained in one of the two covering sets. -/
theorem isPreconnected_iff_subset_of_disjoint_closed :
    IsPreconnected s â†”
      âˆ€ u v, IsClosed u â†’ IsClosed v â†’ s âŠ† u âˆª v â†’ s âˆ© (u âˆ© v) = âˆ… â†’ s âŠ† u âˆ¨ s âŠ† v := by
  constructor <;> intro h
  Â· intro u v hu hv hs huv
    rw [isPreconnected_closed_iff] at h
    specialize h u v hu hv hs
    contrapose! huv
    simp only [not_subset] at huv
    rcases huv with âŸ¨âŸ¨x, hxs, hxuâŸ©, âŸ¨y, hys, hyvâŸ©âŸ©
    have hxv : x âˆˆ v := or_iff_not_imp_left.mp (hs hxs) hxu
    have hyu : y âˆˆ u := or_iff_not_imp_right.mp (hs hys) hyv
    exact h âŸ¨y, hys, hyuâŸ© âŸ¨x, hxs, hxvâŸ©
  Â· rw [isPreconnected_closed_iff]
    intro u v hu hv hs hsu hsv
    by_contra H
    specialize h u v hu hv hs (Set.not_nonempty_iff_eq_empty.mp H)
    apply H
    rcases h with h | h
    Â· rcases hsv with âŸ¨x, hxs, hxvâŸ©
      exact âŸ¨x, hxs, âŸ¨h hxs, hxvâŸ©âŸ©
    Â· rcases hsu with âŸ¨x, hxs, hxuâŸ©
      exact âŸ¨x, hxs, âŸ¨hxu, h hxsâŸ©âŸ©

/-- A closed set `s` is preconnected if and only if for every cover by two closed sets that are
disjoint, it is contained in one of the two covering sets. -/
theorem isPreconnected_iff_subset_of_fully_disjoint_closed {s : Set Î±} (hs : IsClosed s) :
    IsPreconnected s â†”
      âˆ€ u v, IsClosed u â†’ IsClosed v â†’ s âŠ† u âˆª v â†’ Disjoint u v â†’ s âŠ† u âˆ¨ s âŠ† v := by
  refine isPreconnected_iff_subset_of_disjoint_closed.trans âŸ¨?_, ?_âŸ© <;> intro H u v hu hv hss huv
  Â· apply H u v hu hv hss
    rw [huv.inter_eq, inter_empty]
  have H1 := H (u âˆ© s) (v âˆ© s)
  rw [subset_inter_iff, subset_inter_iff] at H1
  simp only [Subset.refl, and_true] at H1
  apply H1 (hu.inter hs) (hv.inter hs)
  Â· rw [â† union_inter_distrib_right]
    exact subset_inter hss Subset.rfl
  Â· rwa [disjoint_iff_inter_eq_empty, â† inter_inter_distrib_right, inter_comm]

theorem IsClopen.connectedComponent_subset {x} (hs : IsClopen s) (hx : x âˆˆ s) :
    connectedComponent x âŠ† s :=
  isPreconnected_connectedComponent.subset_isClopen hs âŸ¨x, mem_connectedComponent, hxâŸ©

/-- The connected component of a point is always a subset of the intersection of all its clopen
neighbourhoods. -/
theorem connectedComponent_subset_iInter_isClopen {x : Î±} :
    connectedComponent x âŠ† â‹‚ Z : { Z : Set Î± // IsClopen Z âˆ§ x âˆˆ Z }, Z :=
  subset_iInter fun Z => Z.2.1.connectedComponent_subset Z.2.2

/-- A clopen set is the union of its connected components. -/
theorem IsClopen.biUnion_connectedComponent_eq {Z : Set Î±} (h : IsClopen Z) :
    â‹ƒ x âˆˆ Z, connectedComponent x = Z :=
  Subset.antisymm (iUnionâ‚‚_subset fun _ => h.connectedComponent_subset) fun _ h =>
    mem_iUnionâ‚‚_of_mem h mem_connectedComponent

open Set.Notation in
/-- If `u v : Set X` and `u âŠ† v` is clopen in `v`, then `u` is the union of the connected
components of `v` in `X` which intersect `u`. -/
lemma IsClopen.biUnion_connectedComponentIn {X : Type*} [TopologicalSpace X] {u v : Set X}
    (hu : IsClopen (v â†“âˆ© u)) (huvâ‚ : u âŠ† v) :
    u = â‹ƒ x âˆˆ u, connectedComponentIn v x := by
  have := congr(((â†‘) : Set v â†’ Set X) $(hu.biUnion_connectedComponent_eq.symm))
  simp only [Subtype.image_preimage_coe, mem_preimage, iUnion_coe_set, image_val_iUnion,
    inter_eq_right.mpr huvâ‚] at this
  nth_rw 1 [this]
  congr! 2 with x hx
  simp only [â† connectedComponentIn_eq_image]
  exact le_antisymm (iUnion_subset fun _ â†¦ le_rfl) <|
    iUnion_subset fun hx â†¦ subset_iUnionâ‚‚_of_subset (huvâ‚ hx) hx le_rfl

variable [TopologicalSpace Î²] {f : Î± â†’ Î²}

/-- The preimage of a connected component is preconnected if the function has connected fibers
and a subset is closed iff the preimage is. -/
theorem preimage_connectedComponent_connected
    (connected_fibers : âˆ€ t : Î², IsConnected (f â»Â¹' {t}))
    (hcl : âˆ€ T : Set Î², IsClosed T â†” IsClosed (f â»Â¹' T)) (t : Î²) :
    IsConnected (f â»Â¹' connectedComponent t) := by
  -- The following proof is essentially https://stacks.math.columbia.edu/tag/0377
  -- although the statement is slightly different
  have hf : Surjective f := Surjective.of_comp fun t : Î² => (connected_fibers t).1
  refine âŸ¨Nonempty.preimage connectedComponent_nonempty hf, ?_âŸ©
  have hT : IsClosed (f â»Â¹' connectedComponent t) :=
    (hcl (connectedComponent t)).1 isClosed_connectedComponent
  -- To show it's preconnected we decompose (f â»Â¹' connectedComponent t) as a subset of two
  -- closed disjoint sets in Î±. We want to show that it's a subset of either.
  rw [isPreconnected_iff_subset_of_fully_disjoint_closed hT]
  intro u v hu hv huv uv_disj
  -- To do this we decompose connectedComponent t into Tâ‚ and Tâ‚‚
  -- we will show that connectedComponent t is a subset of either and hence
  -- (f â»Â¹' connectedComponent t) is a subset of u or v
  let Tâ‚ := { t' âˆˆ connectedComponent t | f â»Â¹' {t'} âŠ† u }
  let Tâ‚‚ := { t' âˆˆ connectedComponent t | f â»Â¹' {t'} âŠ† v }
  have fiber_decomp : âˆ€ t' âˆˆ connectedComponent t, f â»Â¹' {t'} âŠ† u âˆ¨ f â»Â¹' {t'} âŠ† v := by
    intro t' ht'
    apply isPreconnected_iff_subset_of_disjoint_closed.1 (connected_fibers t').2 u v hu hv
    Â· exact Subset.trans (preimage_mono (singleton_subset_iff.2 ht')) huv
    rw [uv_disj.inter_eq, inter_empty]
  have Tâ‚_u : f â»Â¹' Tâ‚ = f â»Â¹' connectedComponent t âˆ© u := by
    apply eq_of_subset_of_subset
    Â· rw [â† biUnion_preimage_singleton]
      refine iUnionâ‚‚_subset fun t' ht' => subset_inter ?_ ht'.2
      rw [hf.preimage_subset_preimage_iff, singleton_subset_iff]
      exact ht'.1
    rintro a âŸ¨hat, hauâŸ©
    constructor
    Â· exact mem_preimage.1 hat
    refine (fiber_decomp (f a) (mem_preimage.1 hat)).resolve_right fun h => ?_
    exact uv_disj.subset_compl_right hau (h rfl)
  -- This proof is exactly the same as the above (modulo some symmetry)
  have Tâ‚‚_v : f â»Â¹' Tâ‚‚ = f â»Â¹' connectedComponent t âˆ© v := by
    apply eq_of_subset_of_subset
    Â· rw [â† biUnion_preimage_singleton]
      refine iUnionâ‚‚_subset fun t' ht' => subset_inter ?_ ht'.2
      rw [hf.preimage_subset_preimage_iff, singleton_subset_iff]
      exact ht'.1
    rintro a âŸ¨hat, havâŸ©
    constructor
    Â· exact mem_preimage.1 hat
    Â· refine (fiber_decomp (f a) (mem_preimage.1 hat)).resolve_left fun h => ?_
      exact uv_disj.subset_compl_left hav (h rfl)
  -- Now we show Tâ‚, Tâ‚‚ are closed, cover connectedComponent t and are disjoint.
  have hTâ‚ : IsClosed Tâ‚ := (hcl Tâ‚).2 (Tâ‚_u.symm â–¸ IsClosed.inter hT hu)
  have hTâ‚‚ : IsClosed Tâ‚‚ := (hcl Tâ‚‚).2 (Tâ‚‚_v.symm â–¸ IsClosed.inter hT hv)
  have T_decomp : connectedComponent t âŠ† Tâ‚ âˆª Tâ‚‚ := fun t' ht' => by
    rw [mem_union t' Tâ‚ Tâ‚‚]
    rcases fiber_decomp t' ht' with htu | htv
    Â· left; exact âŸ¨ht', htuâŸ©
    Â· right; exact âŸ¨ht', htvâŸ©
  have T_disjoint : Disjoint Tâ‚ Tâ‚‚ := by
    refine Disjoint.of_preimage hf ?_
    rw [Tâ‚_u, Tâ‚‚_v, disjoint_iff_inter_eq_empty, â† inter_inter_distrib_left, uv_disj.inter_eq,
      inter_empty]
  -- Now we do cases on whether (connectedComponent t) is a subset of Tâ‚ or Tâ‚‚ to show
  -- that the preimage is a subset of u or v.
  rcases (isPreconnected_iff_subset_of_fully_disjoint_closed isClosed_connectedComponent).1
    isPreconnected_connectedComponent Tâ‚ Tâ‚‚ hTâ‚ hTâ‚‚ T_decomp T_disjoint with h | h
  Â· left
    rw [Subset.antisymm_iff] at Tâ‚_u
    suffices f â»Â¹' connectedComponent t âŠ† f â»Â¹' Tâ‚
      from (this.trans Tâ‚_u.1).trans inter_subset_right
    exact preimage_mono h
  Â· right
    rw [Subset.antisymm_iff] at Tâ‚‚_v
    suffices f â»Â¹' connectedComponent t âŠ† f â»Â¹' Tâ‚‚
      from (this.trans Tâ‚‚_v.1).trans inter_subset_right
    exact preimage_mono h

theorem Topology.IsQuotientMap.preimage_connectedComponent (hf : IsQuotientMap f)
    (h_fibers : âˆ€ y : Î², IsConnected (f â»Â¹' {y})) (a : Î±) :
    f â»Â¹' connectedComponent (f a) = connectedComponent a :=
  ((preimage_connectedComponent_connected h_fibers (fun _ => hf.isClosed_preimage.symm)
      _).subset_connectedComponent mem_connectedComponent).antisymm
    (hf.continuous.mapsTo_connectedComponent a)

lemma Topology.IsQuotientMap.image_connectedComponent {f : Î± â†’ Î²} (hf : IsQuotientMap f)
    (h_fibers : âˆ€ y : Î², IsConnected (f â»Â¹' {y})) (a : Î±) :
    f '' connectedComponent a = connectedComponent (f a) := by
  rw [â† hf.preimage_connectedComponent h_fibers, image_preimage_eq _ hf.surjective]

end Preconnected

section connectedComponentSetoid
/-- The setoid of connected components of a topological space -/
def connectedComponentSetoid (Î± : Type*) [TopologicalSpace Î±] : Setoid Î± :=
  âŸ¨fun x y => connectedComponent x = connectedComponent y,
    âŸ¨fun x => by trivial, fun h1 => h1.symm, fun h1 h2 => h1.trans h2âŸ©âŸ©

/-- The quotient of a space by its connected components -/
def ConnectedComponents (Î± : Type u) [TopologicalSpace Î±] :=
  Quotient (connectedComponentSetoid Î±)

namespace ConnectedComponents

/-- Coercion from a topological space to the set of connected components of this space. -/
def mk : Î± â†’ ConnectedComponents Î± := Quotient.mk''

instance : CoeTC Î± (ConnectedComponents Î±) := âŸ¨mkâŸ©

@[simp]
theorem coe_eq_coe {x y : Î±} :
    (x : ConnectedComponents Î±) = y â†” connectedComponent x = connectedComponent y :=
  Quotient.eq''

theorem coe_ne_coe {x y : Î±} :
    (x : ConnectedComponents Î±) â‰  y â†” connectedComponent x â‰  connectedComponent y :=
  coe_eq_coe.not

theorem coe_eq_coe' {x y : Î±} : (x : ConnectedComponents Î±) = y â†” x âˆˆ connectedComponent y :=
  coe_eq_coe.trans connectedComponent_eq_iff_mem

instance [Inhabited Î±] : Inhabited (ConnectedComponents Î±) :=
  âŸ¨mk defaultâŸ©

instance : TopologicalSpace (ConnectedComponents Î±) :=
  inferInstanceAs (TopologicalSpace (Quotient _))

theorem surjective_coe : Surjective (mk : Î± â†’ ConnectedComponents Î±) :=
  Quot.mk_surjective

theorem isQuotientMap_coe : IsQuotientMap (mk : Î± â†’ ConnectedComponents Î±) :=
  isQuotientMap_quot_mk

@[continuity]
theorem continuous_coe : Continuous (mk : Î± â†’ ConnectedComponents Î±) :=
  isQuotientMap_coe.continuous

@[simp]
theorem range_coe : range (mk : Î± â†’ ConnectedComponents Î±) = univ :=
  surjective_coe.range_eq

end ConnectedComponents

/-- The preimage of a singleton in `connectedComponents` is the connected component
of an element in the equivalence class. -/
theorem connectedComponents_preimage_singleton {x : Î±} :
    (â†‘) â»Â¹' ({â†‘x} : Set (ConnectedComponents Î±)) = connectedComponent x := by
  ext y
  rw [mem_preimage, mem_singleton_iff, ConnectedComponents.coe_eq_coe']

/-- The preimage of the image of a set under the quotient map to `connectedComponents Î±`
is the union of the connected components of the elements in it. -/
theorem connectedComponents_preimage_image (U : Set Î±) :
    (â†‘) â»Â¹' ((â†‘) '' U : Set (ConnectedComponents Î±)) = â‹ƒ x âˆˆ U, connectedComponent x := by
  simp only [connectedComponents_preimage_singleton, preimage_iUnionâ‚‚, image_eq_iUnion]



end connectedComponentSetoid

/-- If every map to `Bool` (a discrete two-element space), that is
continuous on a set `s`, is constant on s, then s is preconnected -/
theorem isPreconnected_of_forall_constant {s : Set Î±}
    (hs : âˆ€ f : Î± â†’ Bool, ContinuousOn f s â†’ âˆ€ x âˆˆ s, âˆ€ y âˆˆ s, f x = f y) : IsPreconnected s := by
  unfold IsPreconnected
  by_contra!
  rcases this with âŸ¨u, v, u_op, v_op, hsuv, âŸ¨x, x_in_s, x_in_uâŸ©, âŸ¨y, y_in_s, y_in_vâŸ©, HâŸ©
  have hy : y âˆ‰ u := fun y_in_u => eq_empty_iff_forall_notMem.mp H y âŸ¨y_in_s, âŸ¨y_in_u, y_in_vâŸ©âŸ©
  have : ContinuousOn u.boolIndicator s := by
    apply (continuousOn_boolIndicator_iff_isClopen _ _).mpr âŸ¨_, _âŸ©
    Â· rw [preimage_subtype_coe_eq_compl hsuv H]
      exact (v_op.preimage continuous_subtype_val).isClosed_compl
    Â· exact u_op.preimage continuous_subtype_val
  simpa [(u.mem_iff_boolIndicator _).mp x_in_u, (u.notMem_iff_boolIndicator _).mp hy] using
    hs _ this x x_in_s y y_in_s

/-- A `PreconnectedSpace` version of `isPreconnected_of_forall_constant` -/
theorem preconnectedSpace_of_forall_constant
    (hs : âˆ€ f : Î± â†’ Bool, Continuous f â†’ âˆ€ x y, f x = f y) : PreconnectedSpace Î± :=
  âŸ¨isPreconnected_of_forall_constant fun f hf x _ y _ =>
      hs f (continuousOn_univ.mp hf) x yâŸ©

theorem preconnectedSpace_iff_clopen :
    PreconnectedSpace Î± â†” âˆ€ s : Set Î±, IsClopen s â†’ s = âˆ… âˆ¨ s = Set.univ := by
  refine âŸ¨fun _ _ => isClopen_iff.mp, fun h â†¦ ?_âŸ©
  refine preconnectedSpace_of_forall_constant fun f hf x y â†¦ ?_
  have : f â»Â¹' {false} = (f â»Â¹' {true})á¶œ := by
    rw [â† Set.preimage_compl, Bool.compl_singleton, Bool.not_true]
  obtain (h | h) := h _ ((isClopen_discrete {true}).preimage hf) <;> simp_all

theorem connectedSpace_iff_clopen :
    ConnectedSpace Î± â†” Nonempty Î± âˆ§ âˆ€ s : Set Î±, IsClopen s â†’ s = âˆ… âˆ¨ s = Set.univ := by
  rw [connectedSpace_iff_univ, IsConnected, â† preconnectedSpace_iff_univ,
    preconnectedSpace_iff_clopen, Set.nonempty_iff_univ_nonempty]

instance [CompactSpace Î±] : CompactSpace <| ConnectedComponents Î± := Quotient.compactSpace
