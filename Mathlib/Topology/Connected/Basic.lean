/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro, Yury Kudryashov
-/
import Mathlib.Data.Set.Image
import Mathlib.Order.SuccPred.Relation
import Mathlib.Topology.Clopen
import Mathlib.Topology.Irreducible

/-!
# Connected subsets of topological spaces

In this file we define connected subsets of a topological spaces and various other properties and
classes related to connectivity.

## Main definitions

We define the following properties for sets in a topological space:

* `IsConnected`: a nonempty set that has no non-trivial open partition.
  See also the section below in the module doc.
* `connectedComponent` is the connected component of an element in the space.

We also have a class stating that the whole space satisfies that property: `ConnectedSpace`

## On the definition of connected sets/spaces

In informal mathematics, connected spaces are assumed to be nonempty.
We formalise the predicate without that assumption as `IsPreconnected`.
In other words, the only difference is whether the empty space counts as connected.
There are good reasons to consider the empty space to be ‚Äútoo simple to be simple‚Äù
See also https://ncatlab.org/nlab/show/too+simple+to+be+simple,
and in particular
https://ncatlab.org/nlab/show/too+simple+to+be+simple#relationship_to_biased_definitions.
-/


open Set Function Topology TopologicalSpace Relation
open scoped Classical

universe u v

variable {Œ± : Type u} {Œ≤ : Type v} {Œπ : Type*} {œÄ : Œπ ‚Üí Type*} [TopologicalSpace Œ±]
  {s t u v : Set Œ±}

section Preconnected

/-- A preconnected set is one where there is no non-trivial open partition. -/
def IsPreconnected (s : Set Œ±) : Prop :=
  ‚àÄ u v : Set Œ±, IsOpen u ‚Üí IsOpen v ‚Üí s ‚äÜ u ‚à™ v ‚Üí (s ‚à© u).Nonempty ‚Üí (s ‚à© v).Nonempty ‚Üí
    (s ‚à© (u ‚à© v)).Nonempty

/-- A connected set is one that is nonempty and where there is no non-trivial open partition. -/
def IsConnected (s : Set Œ±) : Prop :=
  s.Nonempty ‚àß IsPreconnected s

theorem IsConnected.nonempty {s : Set Œ±} (h : IsConnected s) : s.Nonempty :=
  h.1

theorem IsConnected.isPreconnected {s : Set Œ±} (h : IsConnected s) : IsPreconnected s :=
  h.2

theorem IsPreirreducible.isPreconnected {s : Set Œ±} (H : IsPreirreducible s) : IsPreconnected s :=
  fun _ _ hu hv _ => H _ _ hu hv

theorem IsIrreducible.isConnected {s : Set Œ±} (H : IsIrreducible s) : IsConnected s :=
  ‚ü®H.nonempty, H.isPreirreducible.isPreconnected‚ü©

theorem isPreconnected_empty : IsPreconnected (‚àÖ : Set Œ±) :=
  isPreirreducible_empty.isPreconnected

theorem isConnected_singleton {x} : IsConnected ({x} : Set Œ±) :=
  isIrreducible_singleton.isConnected

theorem isPreconnected_singleton {x} : IsPreconnected ({x} : Set Œ±) :=
  isConnected_singleton.isPreconnected

theorem Set.Subsingleton.isPreconnected {s : Set Œ±} (hs : s.Subsingleton) : IsPreconnected s :=
  hs.induction_on isPreconnected_empty fun _ => isPreconnected_singleton

/-- If any point of a set is joined to a fixed point by a preconnected subset,
then the original set is preconnected as well. -/
theorem isPreconnected_of_forall {s : Set Œ±} (x : Œ±)
    (H : ‚àÄ y ‚àà s, ‚àÉ t, t ‚äÜ s ‚àß x ‚àà t ‚àß y ‚àà t ‚àß IsPreconnected t) : IsPreconnected s := by
  rintro u v hu hv hs ‚ü®z, zs, zu‚ü© ‚ü®y, ys, yv‚ü©
  have xs : x ‚àà s := by
    rcases H y ys with ‚ü®t, ts, xt, -, -‚ü©
    exact ts xt
  -- Porting note (#11215): TODO: use `wlog xu : x ‚àà u := hs xs using u v y z, v u z y`
  cases hs xs with
  | inl xu =>
    rcases H y ys with ‚ü®t, ts, xt, yt, ht‚ü©
    have := ht u v hu hv (ts.trans hs) ‚ü®x, xt, xu‚ü© ‚ü®y, yt, yv‚ü©
    exact this.imp fun z hz => ‚ü®ts hz.1, hz.2‚ü©
  | inr xv =>
    rcases H z zs with ‚ü®t, ts, xt, zt, ht‚ü©
    have := ht v u hv hu (ts.trans <| by rwa [union_comm]) ‚ü®x, xt, xv‚ü© ‚ü®z, zt, zu‚ü©
    exact this.imp fun _ h => ‚ü®ts h.1, h.2.2, h.2.1‚ü©

/-- If any two points of a set are contained in a preconnected subset,
then the original set is preconnected as well. -/
theorem isPreconnected_of_forall_pair {s : Set Œ±}
    (H : ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, ‚àÉ t, t ‚äÜ s ‚àß x ‚àà t ‚àß y ‚àà t ‚àß IsPreconnected t) :
    IsPreconnected s := by
  rcases eq_empty_or_nonempty s with (rfl | ‚ü®x, hx‚ü©)
  exacts [isPreconnected_empty, isPreconnected_of_forall x fun y => H x hx y]

/-- A union of a family of preconnected sets with a common point is preconnected as well. -/
theorem isPreconnected_sUnion (x : Œ±) (c : Set (Set Œ±)) (H1 : ‚àÄ s ‚àà c, x ‚àà s)
    (H2 : ‚àÄ s ‚àà c, IsPreconnected s) : IsPreconnected (‚ãÉ‚ÇÄ c) := by
  apply isPreconnected_of_forall x
  rintro y ‚ü®s, sc, ys‚ü©
  exact ‚ü®s, subset_sUnion_of_mem sc, H1 s sc, ys, H2 s sc‚ü©

theorem isPreconnected_iUnion {Œπ : Sort*} {s : Œπ ‚Üí Set Œ±} (h‚ÇÅ : (‚ãÇ i, s i).Nonempty)
    (h‚ÇÇ : ‚àÄ i, IsPreconnected (s i)) : IsPreconnected (‚ãÉ i, s i) :=
  Exists.elim h‚ÇÅ fun f hf => isPreconnected_sUnion f _ hf (forall_mem_range.2 h‚ÇÇ)

theorem IsPreconnected.union (x : Œ±) {s t : Set Œ±} (H1 : x ‚àà s) (H2 : x ‚àà t) (H3 : IsPreconnected s)
    (H4 : IsPreconnected t) : IsPreconnected (s ‚à™ t) :=
  sUnion_pair s t ‚ñ∏ isPreconnected_sUnion x {s, t} (by rintro r (rfl | rfl | h) <;> assumption)
    (by rintro r (rfl | rfl | h) <;> assumption)

theorem IsPreconnected.union' {s t : Set Œ±} (H : (s ‚à© t).Nonempty) (hs : IsPreconnected s)
    (ht : IsPreconnected t) : IsPreconnected (s ‚à™ t) := by
  rcases H with ‚ü®x, hxs, hxt‚ü©
  exact hs.union x hxs hxt ht

theorem IsConnected.union {s t : Set Œ±} (H : (s ‚à© t).Nonempty) (Hs : IsConnected s)
    (Ht : IsConnected t) : IsConnected (s ‚à™ t) := by
  rcases H with ‚ü®x, hx‚ü©
  refine ‚ü®‚ü®x, mem_union_left t (mem_of_mem_inter_left hx)‚ü©, ?_‚ü©
  exact Hs.isPreconnected.union x (mem_of_mem_inter_left hx) (mem_of_mem_inter_right hx)
    Ht.isPreconnected

/-- The directed sUnion of a set S of preconnected subsets is preconnected. -/
theorem IsPreconnected.sUnion_directed {S : Set (Set Œ±)} (K : DirectedOn (¬∑ ‚äÜ ¬∑) S)
    (H : ‚àÄ s ‚àà S, IsPreconnected s) : IsPreconnected (‚ãÉ‚ÇÄ S) := by
  rintro u v hu hv Huv ‚ü®a, ‚ü®s, hsS, has‚ü©, hau‚ü© ‚ü®b, ‚ü®t, htS, hbt‚ü©, hbv‚ü©
  obtain ‚ü®r, hrS, hsr, htr‚ü© : ‚àÉ r ‚àà S, s ‚äÜ r ‚àß t ‚äÜ r := K s hsS t htS
  have Hnuv : (r ‚à© (u ‚à© v)).Nonempty :=
    H _ hrS u v hu hv ((subset_sUnion_of_mem hrS).trans Huv) ‚ü®a, hsr has, hau‚ü© ‚ü®b, htr hbt, hbv‚ü©
  have Kruv : r ‚à© (u ‚à© v) ‚äÜ ‚ãÉ‚ÇÄ S ‚à© (u ‚à© v) := inter_subset_inter_left _ (subset_sUnion_of_mem hrS)
  exact Hnuv.mono Kruv

/-- The biUnion of a family of preconnected sets is preconnected if the graph determined by
whether two sets intersect is preconnected. -/
theorem IsPreconnected.biUnion_of_reflTransGen {Œπ : Type*} {t : Set Œπ} {s : Œπ ‚Üí Set Œ±}
    (H : ‚àÄ i ‚àà t, IsPreconnected (s i))
    (K : ‚àÄ i, i ‚àà t ‚Üí ‚àÄ j, j ‚àà t ‚Üí ReflTransGen (fun i j => (s i ‚à© s j).Nonempty ‚àß i ‚àà t) i j) :
    IsPreconnected (‚ãÉ n ‚àà t, s n) := by
  let R := fun i j : Œπ => (s i ‚à© s j).Nonempty ‚àß i ‚àà t
  have P : ‚àÄ i, i ‚àà t ‚Üí ‚àÄ j, j ‚àà t ‚Üí ReflTransGen R i j ‚Üí
      ‚àÉ p, p ‚äÜ t ‚àß i ‚àà p ‚àß j ‚àà p ‚àß IsPreconnected (‚ãÉ j ‚àà p, s j) := fun i hi j hj h => by
    induction h with
    | refl =>
      refine ‚ü®{i}, singleton_subset_iff.mpr hi, mem_singleton i, mem_singleton i, ?_‚ü©
      rw [biUnion_singleton]
      exact H i hi
    | @tail j k _ hjk ih =>
      obtain ‚ü®p, hpt, hip, hjp, hp‚ü© := ih hjk.2
      refine ‚ü®insert k p, insert_subset_iff.mpr ‚ü®hj, hpt‚ü©, mem_insert_of_mem k hip,
        mem_insert k p, ?_‚ü©
      rw [biUnion_insert]
      refine (H k hj).union' (hjk.1.mono ?_) hp
      rw [inter_comm]
      exact inter_subset_inter_right _ (subset_biUnion_of_mem hjp)
  refine isPreconnected_of_forall_pair ?_
  intro x hx y hy
  obtain ‚ü®i : Œπ, hi : i ‚àà t, hxi : x ‚àà s i‚ü© := mem_iUnion‚ÇÇ.1 hx
  obtain ‚ü®j : Œπ, hj : j ‚àà t, hyj : y ‚àà s j‚ü© := mem_iUnion‚ÇÇ.1 hy
  obtain ‚ü®p, hpt, hip, hjp, hp‚ü© := P i hi j hj (K i hi j hj)
  exact ‚ü®‚ãÉ j ‚àà p, s j, biUnion_subset_biUnion_left hpt, mem_biUnion hip hxi,
    mem_biUnion hjp hyj, hp‚ü©

/-- The biUnion of a family of preconnected sets is preconnected if the graph determined by
whether two sets intersect is preconnected. -/
theorem IsConnected.biUnion_of_reflTransGen {Œπ : Type*} {t : Set Œπ} {s : Œπ ‚Üí Set Œ±}
    (ht : t.Nonempty) (H : ‚àÄ i ‚àà t, IsConnected (s i))
    (K : ‚àÄ i, i ‚àà t ‚Üí ‚àÄ j, j ‚àà t ‚Üí ReflTransGen (fun i j : Œπ => (s i ‚à© s j).Nonempty ‚àß i ‚àà t) i j) :
    IsConnected (‚ãÉ n ‚àà t, s n) :=
  ‚ü®nonempty_biUnion.2 <| ‚ü®ht.some, ht.some_mem, (H _ ht.some_mem).nonempty‚ü©,
    IsPreconnected.biUnion_of_reflTransGen (fun i hi => (H i hi).isPreconnected) K‚ü©

/-- Preconnectedness of the iUnion of a family of preconnected sets
indexed by the vertices of a preconnected graph,
where two vertices are joined when the corresponding sets intersect. -/
theorem IsPreconnected.iUnion_of_reflTransGen {Œπ : Type*} {s : Œπ ‚Üí Set Œ±}
    (H : ‚àÄ i, IsPreconnected (s i))
    (K : ‚àÄ i j, ReflTransGen (fun i j : Œπ => (s i ‚à© s j).Nonempty) i j) :
    IsPreconnected (‚ãÉ n, s n) := by
  rw [‚Üê biUnion_univ]
  exact IsPreconnected.biUnion_of_reflTransGen (fun i _ => H i) fun i _ j _ => by
    simpa [mem_univ] using K i j

theorem IsConnected.iUnion_of_reflTransGen {Œπ : Type*} [Nonempty Œπ] {s : Œπ ‚Üí Set Œ±}
    (H : ‚àÄ i, IsConnected (s i))
    (K : ‚àÄ i j, ReflTransGen (fun i j : Œπ => (s i ‚à© s j).Nonempty) i j) : IsConnected (‚ãÉ n, s n) :=
  ‚ü®nonempty_iUnion.2 <| Nonempty.elim ‚Äπ_‚Ä∫ fun i : Œπ => ‚ü®i, (H _).nonempty‚ü©,
    IsPreconnected.iUnion_of_reflTransGen (fun i => (H i).isPreconnected) K‚ü©

section SuccOrder

open Order

variable [LinearOrder Œ≤] [SuccOrder Œ≤] [IsSuccArchimedean Œ≤]

/-- The iUnion of connected sets indexed by a type with an archimedean successor (like `‚Ñï` or `‚Ñ§`)
  such that any two neighboring sets meet is preconnected. -/
theorem IsPreconnected.iUnion_of_chain {s : Œ≤ ‚Üí Set Œ±} (H : ‚àÄ n, IsPreconnected (s n))
    (K : ‚àÄ n, (s n ‚à© s (succ n)).Nonempty) : IsPreconnected (‚ãÉ n, s n) :=
  IsPreconnected.iUnion_of_reflTransGen H fun i j =>
    reflTransGen_of_succ _ (fun i _ => K i) fun i _ => by
      rw [inter_comm]
      exact K i

/-- The iUnion of connected sets indexed by a type with an archimedean successor (like `‚Ñï` or `‚Ñ§`)
  such that any two neighboring sets meet is connected. -/
theorem IsConnected.iUnion_of_chain [Nonempty Œ≤] {s : Œ≤ ‚Üí Set Œ±} (H : ‚àÄ n, IsConnected (s n))
    (K : ‚àÄ n, (s n ‚à© s (succ n)).Nonempty) : IsConnected (‚ãÉ n, s n) :=
  IsConnected.iUnion_of_reflTransGen H fun i j =>
    reflTransGen_of_succ _ (fun i _ => K i) fun i _ => by
      rw [inter_comm]
      exact K i

/-- The iUnion of preconnected sets indexed by a subset of a type with an archimedean successor
  (like `‚Ñï` or `‚Ñ§`) such that any two neighboring sets meet is preconnected. -/
theorem IsPreconnected.biUnion_of_chain {s : Œ≤ ‚Üí Set Œ±} {t : Set Œ≤} (ht : OrdConnected t)
    (H : ‚àÄ n ‚àà t, IsPreconnected (s n))
    (K : ‚àÄ n : Œ≤, n ‚àà t ‚Üí succ n ‚àà t ‚Üí (s n ‚à© s (succ n)).Nonempty) :
    IsPreconnected (‚ãÉ n ‚àà t, s n) := by
  have h1 : ‚àÄ {i j k : Œ≤}, i ‚àà t ‚Üí j ‚àà t ‚Üí k ‚àà Ico i j ‚Üí k ‚àà t := fun hi hj hk =>
    ht.out hi hj (Ico_subset_Icc_self hk)
  have h2 : ‚àÄ {i j k : Œ≤}, i ‚àà t ‚Üí j ‚àà t ‚Üí k ‚àà Ico i j ‚Üí succ k ‚àà t := fun hi hj hk =>
    ht.out hi hj ‚ü®hk.1.trans <| le_succ _, succ_le_of_lt hk.2‚ü©
  have h3 : ‚àÄ {i j k : Œ≤}, i ‚àà t ‚Üí j ‚àà t ‚Üí k ‚àà Ico i j ‚Üí (s k ‚à© s (succ k)).Nonempty :=
    fun hi hj hk => K _ (h1 hi hj hk) (h2 hi hj hk)
  refine IsPreconnected.biUnion_of_reflTransGen H fun i hi j hj => ?_
  exact reflTransGen_of_succ _ (fun k hk => ‚ü®h3 hi hj hk, h1 hi hj hk‚ü©) fun k hk =>
      ‚ü®by rw [inter_comm]; exact h3 hj hi hk, h2 hj hi hk‚ü©

/-- The iUnion of connected sets indexed by a subset of a type with an archimedean successor
  (like `‚Ñï` or `‚Ñ§`) such that any two neighboring sets meet is preconnected. -/
theorem IsConnected.biUnion_of_chain {s : Œ≤ ‚Üí Set Œ±} {t : Set Œ≤} (hnt : t.Nonempty)
    (ht : OrdConnected t) (H : ‚àÄ n ‚àà t, IsConnected (s n))
    (K : ‚àÄ n : Œ≤, n ‚àà t ‚Üí succ n ‚àà t ‚Üí (s n ‚à© s (succ n)).Nonempty) : IsConnected (‚ãÉ n ‚àà t, s n) :=
  ‚ü®nonempty_biUnion.2 <| ‚ü®hnt.some, hnt.some_mem, (H _ hnt.some_mem).nonempty‚ü©,
    IsPreconnected.biUnion_of_chain ht (fun i hi => (H i hi).isPreconnected) K‚ü©

end SuccOrder

/-- Theorem of bark and tree: if a set is within a preconnected set and its closure, then it is
preconnected as well. See also `IsConnected.subset_closure`. -/
protected theorem IsPreconnected.subset_closure {s : Set Œ±} {t : Set Œ±} (H : IsPreconnected s)
    (Kst : s ‚äÜ t) (Ktcs : t ‚äÜ closure s) : IsPreconnected t :=
  fun u v hu hv htuv ‚ü®_y, hyt, hyu‚ü© ‚ü®_z, hzt, hzv‚ü© =>
  let ‚ü®p, hpu, hps‚ü© := mem_closure_iff.1 (Ktcs hyt) u hu hyu
  let ‚ü®q, hqv, hqs‚ü© := mem_closure_iff.1 (Ktcs hzt) v hv hzv
  let ‚ü®r, hrs, hruv‚ü© := H u v hu hv (Subset.trans Kst htuv) ‚ü®p, hps, hpu‚ü© ‚ü®q, hqs, hqv‚ü©
  ‚ü®r, Kst hrs, hruv‚ü©

/-- Theorem of bark and tree: if a set is within a connected set and its closure, then it is
connected as well. See also `IsPreconnected.subset_closure`. -/
protected theorem IsConnected.subset_closure {s : Set Œ±} {t : Set Œ±} (H : IsConnected s)
    (Kst : s ‚äÜ t) (Ktcs : t ‚äÜ closure s) : IsConnected t :=
  ‚ü®Nonempty.mono Kst H.left, IsPreconnected.subset_closure H.right Kst Ktcs‚ü©

/-- The closure of a preconnected set is preconnected as well. -/
protected theorem IsPreconnected.closure {s : Set Œ±} (H : IsPreconnected s) :
    IsPreconnected (closure s) :=
  IsPreconnected.subset_closure H subset_closure Subset.rfl

/-- The closure of a connected set is connected as well. -/
protected theorem IsConnected.closure {s : Set Œ±} (H : IsConnected s) : IsConnected (closure s) :=
  IsConnected.subset_closure H subset_closure <| Subset.rfl

/-- The image of a preconnected set is preconnected as well. -/
protected theorem IsPreconnected.image [TopologicalSpace Œ≤] {s : Set Œ±} (H : IsPreconnected s)
    (f : Œ± ‚Üí Œ≤) (hf : ContinuousOn f s) : IsPreconnected (f '' s) := by
  -- Unfold/destruct definitions in hypotheses
  rintro u v hu hv huv ‚ü®_, ‚ü®x, xs, rfl‚ü©, xu‚ü© ‚ü®_, ‚ü®y, ys, rfl‚ü©, yv‚ü©
  rcases continuousOn_iff'.1 hf u hu with ‚ü®u', hu', u'_eq‚ü©
  rcases continuousOn_iff'.1 hf v hv with ‚ü®v', hv', v'_eq‚ü©
  -- Reformulate `huv : f '' s ‚äÜ u ‚à™ v` in terms of `u'` and `v'`
  replace huv : s ‚äÜ u' ‚à™ v' := by
    rw [image_subset_iff, preimage_union] at huv
    replace huv := subset_inter huv Subset.rfl
    rw [union_inter_distrib_right, u'_eq, v'_eq, ‚Üê union_inter_distrib_right] at huv
    exact (subset_inter_iff.1 huv).1
  -- Now `s ‚äÜ u' ‚à™ v'`, so we can apply `‚ÄπIsPreconnected s‚Ä∫`
  obtain ‚ü®z, hz‚ü© : (s ‚à© (u' ‚à© v')).Nonempty := by
    refine H u' v' hu' hv' huv ‚ü®x, ?_‚ü© ‚ü®y, ?_‚ü© <;> rw [inter_comm]
    exacts [u'_eq ‚ñ∏ ‚ü®xu, xs‚ü©, v'_eq ‚ñ∏ ‚ü®yv, ys‚ü©]
  rw [‚Üê inter_self s, inter_assoc, inter_left_comm s u', ‚Üê inter_assoc, inter_comm s, inter_comm s,
    ‚Üê u'_eq, ‚Üê v'_eq] at hz
  exact ‚ü®f z, ‚ü®z, hz.1.2, rfl‚ü©, hz.1.1, hz.2.1‚ü©

/-- The image of a connected set is connected as well. -/
protected theorem IsConnected.image [TopologicalSpace Œ≤] {s : Set Œ±} (H : IsConnected s) (f : Œ± ‚Üí Œ≤)
    (hf : ContinuousOn f s) : IsConnected (f '' s) :=
  ‚ü®image_nonempty.mpr H.nonempty, H.isPreconnected.image f hf‚ü©

theorem isPreconnected_closed_iff {s : Set Œ±} :
    IsPreconnected s ‚Üî ‚àÄ t t', IsClosed t ‚Üí IsClosed t' ‚Üí
      s ‚äÜ t ‚à™ t' ‚Üí (s ‚à© t).Nonempty ‚Üí (s ‚à© t').Nonempty ‚Üí (s ‚à© (t ‚à© t')).Nonempty :=
  ‚ü®by
      rintro h t t' ht ht' htt' ‚ü®x, xs, xt‚ü© ‚ü®y, ys, yt'‚ü©
      rw [‚Üê not_disjoint_iff_nonempty_inter, ‚Üê subset_compl_iff_disjoint_right, compl_inter]
      intro h'
      have xt' : x ‚àâ t' := (h' xs).resolve_left (absurd xt)
      have yt : y ‚àâ t := (h' ys).resolve_right (absurd yt')
      have := h _ _ ht.isOpen_compl ht'.isOpen_compl h' ‚ü®y, ys, yt‚ü© ‚ü®x, xs, xt'‚ü©
      rw [‚Üê compl_union] at this
      exact this.ne_empty htt'.disjoint_compl_right.inter_eq,
    by
      rintro h u v hu hv huv ‚ü®x, xs, xu‚ü© ‚ü®y, ys, yv‚ü©
      rw [‚Üê not_disjoint_iff_nonempty_inter, ‚Üê subset_compl_iff_disjoint_right, compl_inter]
      intro h'
      have xv : x ‚àâ v := (h' xs).elim (absurd xu) id
      have yu : y ‚àâ u := (h' ys).elim id (absurd yv)
      have := h _ _ hu.isClosed_compl hv.isClosed_compl h' ‚ü®y, ys, yu‚ü© ‚ü®x, xs, xv‚ü©
      rw [‚Üê compl_union] at this
      exact this.ne_empty huv.disjoint_compl_right.inter_eq‚ü©

theorem Inducing.isPreconnected_image [TopologicalSpace Œ≤] {s : Set Œ±} {f : Œ± ‚Üí Œ≤}
    (hf : Inducing f) : IsPreconnected (f '' s) ‚Üî IsPreconnected s := by
  refine ‚ü®fun h => ?_, fun h => h.image _ hf.continuous.continuousOn‚ü©
  rintro u v hu' hv' huv ‚ü®x, hxs, hxu‚ü© ‚ü®y, hys, hyv‚ü©
  rcases hf.isOpen_iff.1 hu' with ‚ü®u, hu, rfl‚ü©
  rcases hf.isOpen_iff.1 hv' with ‚ü®v, hv, rfl‚ü©
  replace huv : f '' s ‚äÜ u ‚à™ v := by rwa [image_subset_iff]
  rcases h u v hu hv huv ‚ü®f x, mem_image_of_mem _ hxs, hxu‚ü© ‚ü®f y, mem_image_of_mem _ hys, hyv‚ü© with
    ‚ü®_, ‚ü®z, hzs, rfl‚ü©, hzuv‚ü©
  exact ‚ü®z, hzs, hzuv‚ü©

/- TODO: The following lemmas about connection of preimages hold more generally for strict maps
(the quotient and subspace topologies of the image agree) whose fibers are preconnected. -/

theorem IsPreconnected.preimage_of_isOpenMap [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ≤}
    (hs : IsPreconnected s) (hinj : Function.Injective f) (hf : IsOpenMap f) (hsf : s ‚äÜ range f) :
    IsPreconnected (f ‚Åª¬π' s) := fun u v hu hv hsuv hsu hsv => by
  replace hsf : f '' (f ‚Åª¬π' s) = s := image_preimage_eq_of_subset hsf
  obtain ‚ü®_, has, ‚ü®a, hau, rfl‚ü©, hav‚ü© : (s ‚à© (f '' u ‚à© f '' v)).Nonempty := by
    refine hs (f '' u) (f '' v) (hf u hu) (hf v hv) ?_ ?_ ?_
    ¬∑ simpa only [hsf, image_union] using image_subset f hsuv
    ¬∑ simpa only [image_preimage_inter] using hsu.image f
    ¬∑ simpa only [image_preimage_inter] using hsv.image f
  ¬∑ exact ‚ü®a, has, hau, hinj.mem_set_image.1 hav‚ü©

theorem IsPreconnected.preimage_of_isClosedMap [TopologicalSpace Œ≤] {s : Set Œ≤}
    (hs : IsPreconnected s) {f : Œ± ‚Üí Œ≤} (hinj : Function.Injective f) (hf : IsClosedMap f)
    (hsf : s ‚äÜ range f) : IsPreconnected (f ‚Åª¬π' s) :=
  isPreconnected_closed_iff.2 fun u v hu hv hsuv hsu hsv => by
    replace hsf : f '' (f ‚Åª¬π' s) = s := image_preimage_eq_of_subset hsf
    obtain ‚ü®_, has, ‚ü®a, hau, rfl‚ü©, hav‚ü© : (s ‚à© (f '' u ‚à© f '' v)).Nonempty := by
      refine isPreconnected_closed_iff.1 hs (f '' u) (f '' v) (hf u hu) (hf v hv) ?_ ?_ ?_
      ¬∑ simpa only [hsf, image_union] using image_subset f hsuv
      ¬∑ simpa only [image_preimage_inter] using hsu.image f
      ¬∑ simpa only [image_preimage_inter] using hsv.image f
    ¬∑ exact ‚ü®a, has, hau, hinj.mem_set_image.1 hav‚ü©

theorem IsConnected.preimage_of_isOpenMap [TopologicalSpace Œ≤] {s : Set Œ≤} (hs : IsConnected s)
    {f : Œ± ‚Üí Œ≤} (hinj : Function.Injective f) (hf : IsOpenMap f) (hsf : s ‚äÜ range f) :
    IsConnected (f ‚Åª¬π' s) :=
  ‚ü®hs.nonempty.preimage' hsf, hs.isPreconnected.preimage_of_isOpenMap hinj hf hsf‚ü©

theorem IsConnected.preimage_of_isClosedMap [TopologicalSpace Œ≤] {s : Set Œ≤} (hs : IsConnected s)
    {f : Œ± ‚Üí Œ≤} (hinj : Function.Injective f) (hf : IsClosedMap f) (hsf : s ‚äÜ range f) :
    IsConnected (f ‚Åª¬π' s) :=
  ‚ü®hs.nonempty.preimage' hsf, hs.isPreconnected.preimage_of_isClosedMap hinj hf hsf‚ü©

theorem IsPreconnected.subset_or_subset (hu : IsOpen u) (hv : IsOpen v) (huv : Disjoint u v)
    (hsuv : s ‚äÜ u ‚à™ v) (hs : IsPreconnected s) : s ‚äÜ u ‚à® s ‚äÜ v := by
  specialize hs u v hu hv hsuv
  obtain hsu | hsu := (s ‚à© u).eq_empty_or_nonempty
  ¬∑ exact Or.inr ((Set.disjoint_iff_inter_eq_empty.2 hsu).subset_right_of_subset_union hsuv)
  ¬∑ replace hs := mt (hs hsu)
    simp_rw [Set.not_nonempty_iff_eq_empty, ‚Üê Set.disjoint_iff_inter_eq_empty,
      disjoint_iff_inter_eq_empty.1 huv] at hs
    exact Or.inl ((hs s.disjoint_empty).subset_left_of_subset_union hsuv)

theorem IsPreconnected.subset_left_of_subset_union (hu : IsOpen u) (hv : IsOpen v)
    (huv : Disjoint u v) (hsuv : s ‚äÜ u ‚à™ v) (hsu : (s ‚à© u).Nonempty) (hs : IsPreconnected s) :
    s ‚äÜ u :=
  Disjoint.subset_left_of_subset_union hsuv
    (by
      by_contra hsv
      rw [not_disjoint_iff_nonempty_inter] at hsv
      obtain ‚ü®x, _, hx‚ü© := hs u v hu hv hsuv hsu hsv
      exact Set.disjoint_iff.1 huv hx)

theorem IsPreconnected.subset_right_of_subset_union (hu : IsOpen u) (hv : IsOpen v)
    (huv : Disjoint u v) (hsuv : s ‚äÜ u ‚à™ v) (hsv : (s ‚à© v).Nonempty) (hs : IsPreconnected s) :
    s ‚äÜ v :=
  hs.subset_left_of_subset_union hv hu huv.symm (union_comm u v ‚ñ∏ hsuv) hsv

-- Porting note: moved up
/-- Preconnected sets are either contained in or disjoint to any given clopen set. -/
theorem IsPreconnected.subset_isClopen {s t : Set Œ±} (hs : IsPreconnected s) (ht : IsClopen t)
    (hne : (s ‚à© t).Nonempty) : s ‚äÜ t :=
  hs.subset_left_of_subset_union ht.isOpen ht.compl.isOpen disjoint_compl_right (by simp) hne

/-- If a preconnected set `s` intersects an open set `u`, and limit points of `u` inside `s` are
contained in `u`, then the whole set `s` is contained in `u`. -/
theorem IsPreconnected.subset_of_closure_inter_subset (hs : IsPreconnected s) (hu : IsOpen u)
    (h'u : (s ‚à© u).Nonempty) (h : closure u ‚à© s ‚äÜ u) : s ‚äÜ u := by
  have A : s ‚äÜ u ‚à™ (closure u)·∂ú := by
    intro x hx
    by_cases xu : x ‚àà u
    ¬∑ exact Or.inl xu
    ¬∑ right
      intro h'x
      exact xu (h (mem_inter h'x hx))
  apply hs.subset_left_of_subset_union hu isClosed_closure.isOpen_compl _ A h'u
  exact disjoint_compl_right.mono_right (compl_subset_compl.2 subset_closure)

theorem IsPreconnected.prod [TopologicalSpace Œ≤] {s : Set Œ±} {t : Set Œ≤} (hs : IsPreconnected s)
    (ht : IsPreconnected t) : IsPreconnected (s √óÀ¢ t) := by
  apply isPreconnected_of_forall_pair
  rintro ‚ü®a‚ÇÅ, b‚ÇÅ‚ü© ‚ü®ha‚ÇÅ, hb‚ÇÅ‚ü© ‚ü®a‚ÇÇ, b‚ÇÇ‚ü© ‚ü®ha‚ÇÇ, hb‚ÇÇ‚ü©
  refine ‚ü®Prod.mk a‚ÇÅ '' t ‚à™ flip Prod.mk b‚ÇÇ '' s, ?_, .inl ‚ü®b‚ÇÅ, hb‚ÇÅ, rfl‚ü©, .inr ‚ü®a‚ÇÇ, ha‚ÇÇ, rfl‚ü©, ?_‚ü©
  ¬∑ rintro _ (‚ü®y, hy, rfl‚ü© | ‚ü®x, hx, rfl‚ü©)
    exacts [‚ü®ha‚ÇÅ, hy‚ü©, ‚ü®hx, hb‚ÇÇ‚ü©]
  ¬∑ exact (ht.image _ (Continuous.Prod.mk _).continuousOn).union (a‚ÇÅ, b‚ÇÇ) ‚ü®b‚ÇÇ, hb‚ÇÇ, rfl‚ü©
      ‚ü®a‚ÇÅ, ha‚ÇÅ, rfl‚ü© (hs.image _ (continuous_id.prod_mk continuous_const).continuousOn)

theorem IsConnected.prod [TopologicalSpace Œ≤] {s : Set Œ±} {t : Set Œ≤} (hs : IsConnected s)
    (ht : IsConnected t) : IsConnected (s √óÀ¢ t) :=
  ‚ü®hs.1.prod ht.1, hs.2.prod ht.2‚ü©

theorem isPreconnected_univ_pi [‚àÄ i, TopologicalSpace (œÄ i)] {s : ‚àÄ i, Set (œÄ i)}
    (hs : ‚àÄ i, IsPreconnected (s i)) : IsPreconnected (pi univ s) := by
  rintro u v uo vo hsuv ‚ü®f, hfs, hfu‚ü© ‚ü®g, hgs, hgv‚ü©
  rcases exists_finset_piecewise_mem_of_mem_nhds (uo.mem_nhds hfu) g with ‚ü®I, hI‚ü©
  induction' I using Finset.induction_on with i I _ ihI
  ¬∑ refine ‚ü®g, hgs, ‚ü®?_, hgv‚ü©‚ü©
    simpa using hI
  ¬∑ rw [Finset.piecewise_insert] at hI
    have := I.piecewise_mem_set_pi hfs hgs
    refine (hsuv this).elim ihI fun h => ?_
    set S := update (I.piecewise f g) i '' s i
    have hsub : S ‚äÜ pi univ s := by
      refine image_subset_iff.2 fun z hz => ?_
      rwa [update_preimage_univ_pi]
      exact fun j _ => this j trivial
    have hconn : IsPreconnected S :=
      (hs i).image _ (continuous_const.update i continuous_id).continuousOn
    have hSu : (S ‚à© u).Nonempty := ‚ü®_, mem_image_of_mem _ (hfs _ trivial), hI‚ü©
    have hSv : (S ‚à© v).Nonempty := ‚ü®_, ‚ü®_, this _ trivial, update_eq_self _ _‚ü©, h‚ü©
    refine (hconn u v uo vo (hsub.trans hsuv) hSu hSv).mono ?_
    exact inter_subset_inter_left _ hsub

@[simp]
theorem isConnected_univ_pi [‚àÄ i, TopologicalSpace (œÄ i)] {s : ‚àÄ i, Set (œÄ i)} :
    IsConnected (pi univ s) ‚Üî ‚àÄ i, IsConnected (s i) := by
  simp only [IsConnected, ‚Üê univ_pi_nonempty_iff, forall_and, and_congr_right_iff]
  refine fun hne => ‚ü®fun hc i => ?_, isPreconnected_univ_pi‚ü©
  rw [‚Üê eval_image_univ_pi hne]
  exact hc.image _ (continuous_apply _).continuousOn

theorem Sigma.isConnected_iff [‚àÄ i, TopologicalSpace (œÄ i)] {s : Set (Œ£i, œÄ i)} :
    IsConnected s ‚Üî ‚àÉ i t, IsConnected t ‚àß s = Sigma.mk i '' t := by
  refine ‚ü®fun hs => ?_, ?_‚ü©
  ¬∑ obtain ‚ü®‚ü®i, x‚ü©, hx‚ü© := hs.nonempty
    have : s ‚äÜ range (Sigma.mk i) :=
      hs.isPreconnected.subset_isClopen isClopen_range_sigmaMk ‚ü®‚ü®i, x‚ü©, hx, x, rfl‚ü©
    exact ‚ü®i, Sigma.mk i ‚Åª¬π' s, hs.preimage_of_isOpenMap sigma_mk_injective isOpenMap_sigmaMk this,
      (Set.image_preimage_eq_of_subset this).symm‚ü©
  ¬∑ rintro ‚ü®i, t, ht, rfl‚ü©
    exact ht.image _ continuous_sigmaMk.continuousOn

theorem Sigma.isPreconnected_iff [hŒπ : Nonempty Œπ] [‚àÄ i, TopologicalSpace (œÄ i)]
    {s : Set (Œ£i, œÄ i)} : IsPreconnected s ‚Üî ‚àÉ i t, IsPreconnected t ‚àß s = Sigma.mk i '' t := by
  refine ‚ü®fun hs => ?_, ?_‚ü©
  ¬∑ obtain rfl | h := s.eq_empty_or_nonempty
    ¬∑ exact ‚ü®Classical.choice hŒπ, ‚àÖ, isPreconnected_empty, (Set.image_empty _).symm‚ü©
    ¬∑ obtain ‚ü®a, t, ht, rfl‚ü© := Sigma.isConnected_iff.1 ‚ü®h, hs‚ü©
      exact ‚ü®a, t, ht.isPreconnected, rfl‚ü©
  ¬∑ rintro ‚ü®a, t, ht, rfl‚ü©
    exact ht.image _ continuous_sigmaMk.continuousOn

theorem Sum.isConnected_iff [TopologicalSpace Œ≤] {s : Set (Sum Œ± Œ≤)} :
    IsConnected s ‚Üî
      (‚àÉ t, IsConnected t ‚àß s = Sum.inl '' t) ‚à® ‚àÉ t, IsConnected t ‚àß s = Sum.inr '' t := by
  refine ‚ü®fun hs => ?_, ?_‚ü©
  ¬∑ obtain ‚ü®x | x, hx‚ü© := hs.nonempty
    ¬∑ have h : s ‚äÜ range Sum.inl :=
        hs.isPreconnected.subset_isClopen isClopen_range_inl ‚ü®.inl x, hx, x, rfl‚ü©
      refine Or.inl ‚ü®Sum.inl ‚Åª¬π' s, ?_, ?_‚ü©
      ¬∑ exact hs.preimage_of_isOpenMap Sum.inl_injective isOpenMap_inl h
      ¬∑ exact (image_preimage_eq_of_subset h).symm
    ¬∑ have h : s ‚äÜ range Sum.inr :=
        hs.isPreconnected.subset_isClopen isClopen_range_inr ‚ü®.inr x, hx, x, rfl‚ü©
      refine Or.inr ‚ü®Sum.inr ‚Åª¬π' s, ?_, ?_‚ü©
      ¬∑ exact hs.preimage_of_isOpenMap Sum.inr_injective isOpenMap_inr h
      ¬∑ exact (image_preimage_eq_of_subset h).symm
  ¬∑ rintro (‚ü®t, ht, rfl‚ü© | ‚ü®t, ht, rfl‚ü©)
    ¬∑ exact ht.image _ continuous_inl.continuousOn
    ¬∑ exact ht.image _ continuous_inr.continuousOn

theorem Sum.isPreconnected_iff [TopologicalSpace Œ≤] {s : Set (Sum Œ± Œ≤)} :
    IsPreconnected s ‚Üî
      (‚àÉ t, IsPreconnected t ‚àß s = Sum.inl '' t) ‚à® ‚àÉ t, IsPreconnected t ‚àß s = Sum.inr '' t := by
  refine ‚ü®fun hs => ?_, ?_‚ü©
  ¬∑ obtain rfl | h := s.eq_empty_or_nonempty
    ¬∑ exact Or.inl ‚ü®‚àÖ, isPreconnected_empty, (Set.image_empty _).symm‚ü©
    obtain ‚ü®t, ht, rfl‚ü© | ‚ü®t, ht, rfl‚ü© := Sum.isConnected_iff.1 ‚ü®h, hs‚ü©
    ¬∑ exact Or.inl ‚ü®t, ht.isPreconnected, rfl‚ü©
    ¬∑ exact Or.inr ‚ü®t, ht.isPreconnected, rfl‚ü©
  ¬∑ rintro (‚ü®t, ht, rfl‚ü© | ‚ü®t, ht, rfl‚ü©)
    ¬∑ exact ht.image _ continuous_inl.continuousOn
    ¬∑ exact ht.image _ continuous_inr.continuousOn

/-- The connected component of a point is the maximal connected set
that contains this point. -/
def connectedComponent (x : Œ±) : Set Œ± :=
  ‚ãÉ‚ÇÄ { s : Set Œ± | IsPreconnected s ‚àß x ‚àà s }

/-- Given a set `F` in a topological space `Œ±` and a point `x : Œ±`, the connected
component of `x` in `F` is the connected component of `x` in the subtype `F` seen as
a set in `Œ±`. This definition does not make sense if `x` is not in `F` so we return the
empty set in this case. -/
def connectedComponentIn (F : Set Œ±) (x : Œ±) : Set Œ± :=
  if h : x ‚àà F then (‚Üë) '' connectedComponent (‚ü®x, h‚ü© : F) else ‚àÖ

theorem connectedComponentIn_eq_image {F : Set Œ±} {x : Œ±} (h : x ‚àà F) :
    connectedComponentIn F x = (‚Üë) '' connectedComponent (‚ü®x, h‚ü© : F) :=
  dif_pos h

theorem connectedComponentIn_eq_empty {F : Set Œ±} {x : Œ±} (h : x ‚àâ F) :
    connectedComponentIn F x = ‚àÖ :=
  dif_neg h

theorem mem_connectedComponent {x : Œ±} : x ‚àà connectedComponent x :=
  mem_sUnion_of_mem (mem_singleton x) ‚ü®isPreconnected_singleton, mem_singleton x‚ü©

theorem mem_connectedComponentIn {x : Œ±} {F : Set Œ±} (hx : x ‚àà F) :
    x ‚àà connectedComponentIn F x := by
  simp [connectedComponentIn_eq_image hx, mem_connectedComponent, hx]

theorem connectedComponent_nonempty {x : Œ±} : (connectedComponent x).Nonempty :=
  ‚ü®x, mem_connectedComponent‚ü©

theorem connectedComponentIn_nonempty_iff {x : Œ±} {F : Set Œ±} :
    (connectedComponentIn F x).Nonempty ‚Üî x ‚àà F := by
  rw [connectedComponentIn]
  split_ifs <;> simp [connectedComponent_nonempty, *]

theorem connectedComponentIn_subset (F : Set Œ±) (x : Œ±) : connectedComponentIn F x ‚äÜ F := by
  rw [connectedComponentIn]
  split_ifs <;> simp

theorem isPreconnected_connectedComponent {x : Œ±} : IsPreconnected (connectedComponent x) :=
  isPreconnected_sUnion x _ (fun _ => And.right) fun _ => And.left

theorem isPreconnected_connectedComponentIn {x : Œ±} {F : Set Œ±} :
    IsPreconnected (connectedComponentIn F x) := by
  rw [connectedComponentIn]; split_ifs
  ¬∑ exact inducing_subtype_val.isPreconnected_image.mpr isPreconnected_connectedComponent
  ¬∑ exact isPreconnected_empty

theorem isConnected_connectedComponent {x : Œ±} : IsConnected (connectedComponent x) :=
  ‚ü®‚ü®x, mem_connectedComponent‚ü©, isPreconnected_connectedComponent‚ü©

theorem isConnected_connectedComponentIn_iff {x : Œ±} {F : Set Œ±} :
    IsConnected (connectedComponentIn F x) ‚Üî x ‚àà F := by
  simp_rw [‚Üê connectedComponentIn_nonempty_iff, IsConnected, isPreconnected_connectedComponentIn,
    and_true_iff]

theorem IsPreconnected.subset_connectedComponent {x : Œ±} {s : Set Œ±} (H1 : IsPreconnected s)
    (H2 : x ‚àà s) : s ‚äÜ connectedComponent x := fun _z hz => mem_sUnion_of_mem hz ‚ü®H1, H2‚ü©

theorem IsPreconnected.subset_connectedComponentIn {x : Œ±} {F : Set Œ±} (hs : IsPreconnected s)
    (hxs : x ‚àà s) (hsF : s ‚äÜ F) : s ‚äÜ connectedComponentIn F x := by
  have : IsPreconnected (((‚Üë) : F ‚Üí Œ±) ‚Åª¬π' s) := by
    refine inducing_subtype_val.isPreconnected_image.mp ?_
    rwa [Subtype.image_preimage_coe, inter_eq_right.mpr hsF]
  have h2xs : (‚ü®x, hsF hxs‚ü© : F) ‚àà (‚Üë) ‚Åª¬π' s := by
    rw [mem_preimage]
    exact hxs
  have := this.subset_connectedComponent h2xs
  rw [connectedComponentIn_eq_image (hsF hxs)]
  refine Subset.trans ?_ (image_subset _ this)
  rw [Subtype.image_preimage_coe, inter_eq_right.mpr hsF]

theorem IsConnected.subset_connectedComponent {x : Œ±} {s : Set Œ±} (H1 : IsConnected s)
    (H2 : x ‚àà s) : s ‚äÜ connectedComponent x :=
  H1.2.subset_connectedComponent H2

theorem IsPreconnected.connectedComponentIn {x : Œ±} {F : Set Œ±} (h : IsPreconnected F)
    (hx : x ‚àà F) : connectedComponentIn F x = F :=
  (connectedComponentIn_subset F x).antisymm (h.subset_connectedComponentIn hx subset_rfl)

theorem connectedComponent_eq {x y : Œ±} (h : y ‚àà connectedComponent x) :
    connectedComponent x = connectedComponent y :=
  eq_of_subset_of_subset (isConnected_connectedComponent.subset_connectedComponent h)
    (isConnected_connectedComponent.subset_connectedComponent
      (Set.mem_of_mem_of_subset mem_connectedComponent
        (isConnected_connectedComponent.subset_connectedComponent h)))

theorem connectedComponent_eq_iff_mem {x y : Œ±} :
    connectedComponent x = connectedComponent y ‚Üî x ‚àà connectedComponent y :=
  ‚ü®fun h => h ‚ñ∏ mem_connectedComponent, fun h => (connectedComponent_eq h).symm‚ü©

theorem connectedComponentIn_eq {x y : Œ±} {F : Set Œ±} (h : y ‚àà connectedComponentIn F x) :
    connectedComponentIn F x = connectedComponentIn F y := by
  have hx : x ‚àà F := connectedComponentIn_nonempty_iff.mp ‚ü®y, h‚ü©
  simp_rw [connectedComponentIn_eq_image hx] at h ‚ä¢
  obtain ‚ü®‚ü®y, hy‚ü©, h2y, rfl‚ü© := h
  simp_rw [connectedComponentIn_eq_image hy, connectedComponent_eq h2y]

theorem connectedComponentIn_univ (x : Œ±) : connectedComponentIn univ x = connectedComponent x :=
  subset_antisymm
    (isPreconnected_connectedComponentIn.subset_connectedComponent <|
      mem_connectedComponentIn trivial)
    (isPreconnected_connectedComponent.subset_connectedComponentIn mem_connectedComponent <|
      subset_univ _)

theorem connectedComponent_disjoint {x y : Œ±} (h : connectedComponent x ‚â† connectedComponent y) :
    Disjoint (connectedComponent x) (connectedComponent y) :=
  Set.disjoint_left.2 fun _ h1 h2 =>
    h ((connectedComponent_eq h1).trans (connectedComponent_eq h2).symm)

theorem isClosed_connectedComponent {x : Œ±} : IsClosed (connectedComponent x) :=
  closure_subset_iff_isClosed.1 <|
    isConnected_connectedComponent.closure.subset_connectedComponent <|
      subset_closure mem_connectedComponent

theorem Continuous.image_connectedComponent_subset [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤}
    (h : Continuous f) (a : Œ±) : f '' connectedComponent a ‚äÜ connectedComponent (f a) :=
  (isConnected_connectedComponent.image f h.continuousOn).subset_connectedComponent
    ((mem_image f (connectedComponent a) (f a)).2 ‚ü®a, mem_connectedComponent, rfl‚ü©)

theorem Continuous.image_connectedComponentIn_subset [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ±}
    {a : Œ±} (hf : Continuous f) (hx : a ‚àà s) :
    f '' connectedComponentIn s a ‚äÜ connectedComponentIn (f '' s) (f a) :=
  (isPreconnected_connectedComponentIn.image _ hf.continuousOn).subset_connectedComponentIn
    (mem_image_of_mem _ <| mem_connectedComponentIn hx)
    (image_subset _ <| connectedComponentIn_subset _ _)

theorem Continuous.mapsTo_connectedComponent [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤} (h : Continuous f)
    (a : Œ±) : MapsTo f (connectedComponent a) (connectedComponent (f a)) :=
  mapsTo'.2 <| h.image_connectedComponent_subset a

theorem Continuous.mapsTo_connectedComponentIn [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ±}
    (h : Continuous f) {a : Œ±} (hx : a ‚àà s) :
    MapsTo f (connectedComponentIn s a) (connectedComponentIn (f '' s) (f a)) :=
  mapsTo'.2 <| image_connectedComponentIn_subset h hx

theorem irreducibleComponent_subset_connectedComponent {x : Œ±} :
    irreducibleComponent x ‚äÜ connectedComponent x :=
  isIrreducible_irreducibleComponent.isConnected.subset_connectedComponent mem_irreducibleComponent

@[mono]
theorem connectedComponentIn_mono (x : Œ±) {F G : Set Œ±} (h : F ‚äÜ G) :
    connectedComponentIn F x ‚äÜ connectedComponentIn G x := by
  by_cases hx : x ‚àà F
  ¬∑ rw [connectedComponentIn_eq_image hx, connectedComponentIn_eq_image (h hx), ‚Üê
      show ((‚Üë) : G ‚Üí Œ±) ‚àò inclusion h = (‚Üë) from rfl, image_comp]
    exact image_subset _ ((continuous_inclusion h).image_connectedComponent_subset ‚ü®x, hx‚ü©)
  ¬∑ rw [connectedComponentIn_eq_empty hx]
    exact Set.empty_subset _

/-- A preconnected space is one where there is no non-trivial open partition. -/
class PreconnectedSpace (Œ± : Type u) [TopologicalSpace Œ±] : Prop where
  /-- The universal set `Set.univ` in a preconnected space is a preconnected set. -/
  isPreconnected_univ : IsPreconnected (univ : Set Œ±)

export PreconnectedSpace (isPreconnected_univ)

/-- A connected space is a nonempty one where there is no non-trivial open partition. -/
class ConnectedSpace (Œ± : Type u) [TopologicalSpace Œ±] extends PreconnectedSpace Œ± : Prop where
  /-- A connected space is nonempty. -/
  toNonempty : Nonempty Œ±

attribute [instance 50] ConnectedSpace.toNonempty  -- see Note [lower instance priority]

-- see Note [lower instance priority]
theorem isConnected_univ [ConnectedSpace Œ±] : IsConnected (univ : Set Œ±) :=
  ‚ü®univ_nonempty, isPreconnected_univ‚ü©

lemma preconnectedSpace_iff_univ : PreconnectedSpace Œ± ‚Üî IsPreconnected (univ : Set Œ±) :=
  ‚ü®fun h ‚Ü¶ h.1, fun h ‚Ü¶ ‚ü®h‚ü©‚ü©

lemma connectedSpace_iff_univ : ConnectedSpace Œ± ‚Üî IsConnected (univ : Set Œ±) :=
  ‚ü®fun h ‚Ü¶ ‚ü®univ_nonempty, h.1.1‚ü©,
   fun h ‚Ü¶ ConnectedSpace.mk (toPreconnectedSpace := ‚ü®h.2‚ü©) ‚ü®h.1.some‚ü©‚ü©

theorem isPreconnected_range [TopologicalSpace Œ≤] [PreconnectedSpace Œ±] {f : Œ± ‚Üí Œ≤}
    (h : Continuous f) : IsPreconnected (range f) :=
  @image_univ _ _ f ‚ñ∏ isPreconnected_univ.image _ h.continuousOn

theorem isConnected_range [TopologicalSpace Œ≤] [ConnectedSpace Œ±] {f : Œ± ‚Üí Œ≤} (h : Continuous f) :
    IsConnected (range f) :=
  ‚ü®range_nonempty f, isPreconnected_range h‚ü©

theorem Function.Surjective.connectedSpace [ConnectedSpace Œ±] [TopologicalSpace Œ≤]
    {f : Œ± ‚Üí Œ≤} (hf : Surjective f) (hf' : Continuous f) : ConnectedSpace Œ≤ := by
  rw [connectedSpace_iff_univ, ‚Üê hf.range_eq]
  exact isConnected_range hf'

instance Quotient.instConnectedSpace {s : Setoid Œ±} [ConnectedSpace Œ±] :
    ConnectedSpace (Quotient s) :=
  (surjective_quotient_mk' _).connectedSpace continuous_coinduced_rng

theorem DenseRange.preconnectedSpace [TopologicalSpace Œ≤] [PreconnectedSpace Œ±] {f : Œ± ‚Üí Œ≤}
    (hf : DenseRange f) (hc : Continuous f) : PreconnectedSpace Œ≤ :=
  ‚ü®hf.closure_eq ‚ñ∏ (isPreconnected_range hc).closure‚ü©

theorem connectedSpace_iff_connectedComponent :
    ConnectedSpace Œ± ‚Üî ‚àÉ x : Œ±, connectedComponent x = univ := by
  constructor
  ¬∑ rintro ‚ü®‚ü®x‚ü©‚ü©
    exact
      ‚ü®x, eq_univ_of_univ_subset <| isPreconnected_univ.subset_connectedComponent (mem_univ x)‚ü©
  ¬∑ rintro ‚ü®x, h‚ü©
    haveI : PreconnectedSpace Œ± :=
      ‚ü®by rw [‚Üê h]; exact isPreconnected_connectedComponent‚ü©
    exact ‚ü®‚ü®x‚ü©‚ü©

theorem preconnectedSpace_iff_connectedComponent :
    PreconnectedSpace Œ± ‚Üî ‚àÄ x : Œ±, connectedComponent x = univ := by
  constructor
  ¬∑ intro h x
    exact eq_univ_of_univ_subset <| isPreconnected_univ.subset_connectedComponent (mem_univ x)
  ¬∑ intro h
    cases' isEmpty_or_nonempty Œ± with hŒ± hŒ±
    ¬∑ exact ‚ü®by rw [univ_eq_empty_iff.mpr hŒ±]; exact isPreconnected_empty‚ü©
    ¬∑ exact ‚ü®by rw [‚Üê h (Classical.choice hŒ±)]; exact isPreconnected_connectedComponent‚ü©

@[simp]
theorem PreconnectedSpace.connectedComponent_eq_univ {X : Type*} [TopologicalSpace X]
    [h : PreconnectedSpace X] (x : X) : connectedComponent x = univ :=
  preconnectedSpace_iff_connectedComponent.mp h x

instance [TopologicalSpace Œ≤] [PreconnectedSpace Œ±] [PreconnectedSpace Œ≤] :
    PreconnectedSpace (Œ± √ó Œ≤) :=
  ‚ü®by
    rw [‚Üê univ_prod_univ]
    exact isPreconnected_univ.prod isPreconnected_univ‚ü©

instance [TopologicalSpace Œ≤] [ConnectedSpace Œ±] [ConnectedSpace Œ≤] : ConnectedSpace (Œ± √ó Œ≤) :=
  ‚ü®inferInstance‚ü©

instance [‚àÄ i, TopologicalSpace (œÄ i)] [‚àÄ i, PreconnectedSpace (œÄ i)] :
    PreconnectedSpace (‚àÄ i, œÄ i) :=
  ‚ü®by rw [‚Üê pi_univ univ]; exact isPreconnected_univ_pi fun i => isPreconnected_univ‚ü©

instance [‚àÄ i, TopologicalSpace (œÄ i)] [‚àÄ i, ConnectedSpace (œÄ i)] : ConnectedSpace (‚àÄ i, œÄ i) :=
  ‚ü®inferInstance‚ü©

-- see Note [lower instance priority]
instance (priority := 100) PreirreducibleSpace.preconnectedSpace (Œ± : Type u) [TopologicalSpace Œ±]
    [PreirreducibleSpace Œ±] : PreconnectedSpace Œ± :=
  ‚ü®isPreirreducible_univ.isPreconnected‚ü©

-- see Note [lower instance priority]
instance (priority := 100) IrreducibleSpace.connectedSpace (Œ± : Type u) [TopologicalSpace Œ±]
    [IrreducibleSpace Œ±] : ConnectedSpace Œ± where toNonempty := IrreducibleSpace.toNonempty

/-- A continuous map from a connected space to a disjoint union `Œ£ i, œÄ i` can be lifted to one of
the components `œÄ i`. See also `ContinuousMap.exists_lift_sigma` for a version with bundled
`ContinuousMap`s. -/
theorem Continuous.exists_lift_sigma [ConnectedSpace Œ±] [‚àÄ i, TopologicalSpace (œÄ i)]
    {f : Œ± ‚Üí Œ£ i, œÄ i} (hf : Continuous f) :
    ‚àÉ (i : Œπ) (g : Œ± ‚Üí œÄ i), Continuous g ‚àß f = Sigma.mk i ‚àò g := by
  obtain ‚ü®i, hi‚ü© : ‚àÉ i, range f ‚äÜ range (.mk i) := by
    rcases Sigma.isConnected_iff.1 (isConnected_range hf) with ‚ü®i, s, -, hs‚ü©
    exact ‚ü®i, hs.trans_subset (image_subset_range _ _)‚ü©
  rcases range_subset_range_iff_exists_comp.1 hi with ‚ü®g, rfl‚ü©
  refine ‚ü®i, g, ?_, rfl‚ü©
  rwa [‚Üê embedding_sigmaMk.continuous_iff] at hf

theorem nonempty_inter [PreconnectedSpace Œ±] {s t : Set Œ±} :
    IsOpen s ‚Üí IsOpen t ‚Üí s ‚à™ t = univ ‚Üí s.Nonempty ‚Üí t.Nonempty ‚Üí (s ‚à© t).Nonempty := by
  simpa only [univ_inter, univ_subset_iff] using @PreconnectedSpace.isPreconnected_univ Œ± _ _ s t

theorem isClopen_iff [PreconnectedSpace Œ±] {s : Set Œ±} : IsClopen s ‚Üî s = ‚àÖ ‚à® s = univ :=
  ‚ü®fun hs =>
    by_contradiction fun h =>
      have h1 : s ‚â† ‚àÖ ‚àß s·∂ú ‚â† ‚àÖ :=
        ‚ü®mt Or.inl h,
          mt (fun h2 => Or.inr <| (by rw [‚Üê compl_compl s, h2, compl_empty] : s = univ)) h‚ü©
      let ‚ü®_, h2, h3‚ü© :=
        nonempty_inter hs.2 hs.1.isOpen_compl (union_compl_self s) (nonempty_iff_ne_empty.2 h1.1)
          (nonempty_iff_ne_empty.2 h1.2)
      h3 h2,
    by rintro (rfl | rfl) <;> [exact isClopen_empty; exact isClopen_univ]‚ü©

theorem IsClopen.eq_univ [PreconnectedSpace Œ±] {s : Set Œ±} (h' : IsClopen s) (h : s.Nonempty) :
    s = univ :=
  (isClopen_iff.mp h').resolve_left h.ne_empty

section disjoint_subsets

variable [PreconnectedSpace Œ±]
  {s : Œπ ‚Üí Set Œ±} (h_nonempty : ‚àÄ i, (s i).Nonempty) (h_disj : Pairwise (Disjoint on s))

/-- In a preconnected space, any disjoint family of non-empty clopen subsets has at most one
element. -/
lemma subsingleton_of_disjoint_isClopen
    (h_clopen : ‚àÄ i, IsClopen (s i)) :
    Subsingleton Œπ := by
  replace h_nonempty : ‚àÄ i, s i ‚â† ‚àÖ := by intro i; rw [‚Üê nonempty_iff_ne_empty]; exact h_nonempty i
  rw [‚Üê not_nontrivial_iff_subsingleton]
  by_contra contra
  obtain ‚ü®i, j, h_ne‚ü© := contra
  replace h_ne : s i ‚à© s j = ‚àÖ := by
    simpa only [‚Üê bot_eq_empty, eq_bot_iff, ‚Üê inf_eq_inter, ‚Üê disjoint_iff_inf_le] using h_disj h_ne
  cases' isClopen_iff.mp (h_clopen i) with hi hi
  ¬∑ exact h_nonempty i hi
  ¬∑ rw [hi, univ_inter] at h_ne
    exact h_nonempty j h_ne

/-- In a preconnected space, any disjoint cover by non-empty open subsets has at most one
element. -/
lemma subsingleton_of_disjoint_isOpen_iUnion_eq_univ
    (h_open : ‚àÄ i, IsOpen (s i)) (h_Union : ‚ãÉ i, s i = univ) :
    Subsingleton Œπ := by
  refine subsingleton_of_disjoint_isClopen h_nonempty h_disj (fun i ‚Ü¶ ‚ü®?_, h_open i‚ü©)
  rw [‚Üê isOpen_compl_iff, compl_eq_univ_diff, ‚Üê h_Union, iUnion_diff]
  refine isOpen_iUnion (fun j ‚Ü¶ ?_)
  rcases eq_or_ne i j with rfl | h_ne
  ¬∑ simp
  ¬∑ simpa only [(h_disj h_ne.symm).sdiff_eq_left] using h_open j

/-- In a preconnected space, any finite disjoint cover by non-empty closed subsets has at most one
element. -/
lemma subsingleton_of_disjoint_isClosed_iUnion_eq_univ [Finite Œπ]
    (h_closed : ‚àÄ i, IsClosed (s i)) (h_Union : ‚ãÉ i, s i = univ) :
    Subsingleton Œπ := by
  refine subsingleton_of_disjoint_isClopen h_nonempty h_disj (fun i ‚Ü¶ ‚ü®h_closed i, ?_‚ü©)
  rw [‚Üê isClosed_compl_iff, compl_eq_univ_diff, ‚Üê h_Union, iUnion_diff]
  refine isClosed_iUnion_of_finite (fun j ‚Ü¶ ?_)
  rcases eq_or_ne i j with rfl | h_ne
  ¬∑ simp
  ¬∑ simpa only [(h_disj h_ne.symm).sdiff_eq_left] using h_closed j

end disjoint_subsets

theorem frontier_eq_empty_iff [PreconnectedSpace Œ±] {s : Set Œ±} :
    frontier s = ‚àÖ ‚Üî s = ‚àÖ ‚à® s = univ :=
  isClopen_iff_frontier_eq_empty.symm.trans isClopen_iff

theorem nonempty_frontier_iff [PreconnectedSpace Œ±] {s : Set Œ±} :
    (frontier s).Nonempty ‚Üî s.Nonempty ‚àß s ‚â† univ := by
  simp only [nonempty_iff_ne_empty, Ne, frontier_eq_empty_iff, not_or]

theorem Subtype.preconnectedSpace {s : Set Œ±} (h : IsPreconnected s) : PreconnectedSpace s where
  isPreconnected_univ := by
    rwa [‚Üê inducing_subtype_val.isPreconnected_image, image_univ, Subtype.range_val]

theorem Subtype.connectedSpace {s : Set Œ±} (h : IsConnected s) : ConnectedSpace s where
  toPreconnectedSpace := Subtype.preconnectedSpace h.isPreconnected
  toNonempty := h.nonempty.to_subtype

theorem isPreconnected_iff_preconnectedSpace {s : Set Œ±} : IsPreconnected s ‚Üî PreconnectedSpace s :=
  ‚ü®Subtype.preconnectedSpace, fun h => by
    simpa using isPreconnected_univ.image ((‚Üë) : s ‚Üí Œ±) continuous_subtype_val.continuousOn‚ü©

theorem isConnected_iff_connectedSpace {s : Set Œ±} : IsConnected s ‚Üî ConnectedSpace s :=
  ‚ü®Subtype.connectedSpace, fun h =>
    ‚ü®nonempty_subtype.mp h.2, isPreconnected_iff_preconnectedSpace.mpr h.1‚ü©‚ü©

/-- In a preconnected space, given a transitive relation `P`, if `P x y` and `P y x` are true
for `y` close enough to `x`, then `P x y` holds for all `x, y`. This is a version of the fact
that, if an equivalence relation has open classes, then it has a single equivalence class. -/
lemma PreconnectedSpace.induction‚ÇÇ' [PreconnectedSpace Œ±] (P : Œ± ‚Üí Œ± ‚Üí Prop)
    (h : ‚àÄ x, ‚àÄ·∂† y in ùìù x, P x y ‚àß P y x) (h' : Transitive P) (x y : Œ±) :
    P x y := by
  let u := {z | P x z}
  have A : IsClosed u := by
    apply isClosed_iff_nhds.2 (fun z hz ‚Ü¶ ?_)
    rcases hz _ (h z) with ‚ü®t, ht, h't‚ü©
    exact h' h't ht.2
  have B : IsOpen u := by
    apply isOpen_iff_mem_nhds.2 (fun z hz ‚Ü¶ ?_)
    filter_upwards [h z] with t ht
    exact h' hz ht.1
  have C : u.Nonempty := ‚ü®x, (mem_of_mem_nhds (h x)).1‚ü©
  have D : u = Set.univ := IsClopen.eq_univ ‚ü®A, B‚ü© C
  show y ‚àà u
  simp [D]

/-- In a preconnected space, if a symmetric transitive relation `P x y` is true for `y` close
enough to `x`, then it holds for all `x, y`. This is a version of the fact that, if an equivalence
relation has open classes, then it has a single equivalence class. -/
lemma PreconnectedSpace.induction‚ÇÇ [PreconnectedSpace Œ±] (P : Œ± ‚Üí Œ± ‚Üí Prop)
    (h : ‚àÄ x, ‚àÄ·∂† y in ùìù x, P x y) (h' : Transitive P) (h'' : Symmetric P) (x y : Œ±) :
    P x y := by
  refine PreconnectedSpace.induction‚ÇÇ' P (fun z ‚Ü¶ ?_) h' x y
  filter_upwards [h z] with a ha
  exact ‚ü®ha, h'' ha‚ü©

/-- In a preconnected set, given a transitive relation `P`, if `P x y` and `P y x` are true
for `y` close enough to `x`, then `P x y` holds for all `x, y`. This is a version of the fact
that, if an equivalence relation has open classes, then it has a single equivalence class. -/
lemma IsPreconnected.induction‚ÇÇ' {s : Set Œ±} (hs : IsPreconnected s) (P : Œ± ‚Üí Œ± ‚Üí Prop)
    (h : ‚àÄ x ‚àà s, ‚àÄ·∂† y in ùìù[s] x, P x y ‚àß P y x)
    (h' : ‚àÄ x y z, x ‚àà s ‚Üí y ‚àà s ‚Üí z ‚àà s ‚Üí P x y ‚Üí P y z ‚Üí P x z)
    {x y : Œ±} (hx : x ‚àà s) (hy : y ‚àà s) : P x y := by
  let Q : s ‚Üí s ‚Üí Prop := fun a b ‚Ü¶ P a b
  show Q ‚ü®x, hx‚ü© ‚ü®y, hy‚ü©
  have : PreconnectedSpace s := Subtype.preconnectedSpace hs
  apply PreconnectedSpace.induction‚ÇÇ'
  ¬∑ rintro ‚ü®x, hx‚ü©
    have Z := h x hx
    rwa [nhdsWithin_eq_map_subtype_coe] at Z
  ¬∑ rintro ‚ü®a, ha‚ü© ‚ü®b, hb‚ü© ‚ü®c, hc‚ü© hab hbc
    exact h' a b c ha hb hc hab hbc

/-- In a preconnected set, if a symmetric transitive relation `P x y` is true for `y` close
enough to `x`, then it holds for all `x, y`. This is a version of the fact that, if an equivalence
relation has open classes, then it has a single equivalence class. -/
lemma IsPreconnected.induction‚ÇÇ {s : Set Œ±} (hs : IsPreconnected s) (P : Œ± ‚Üí Œ± ‚Üí Prop)
    (h : ‚àÄ x ‚àà s, ‚àÄ·∂† y in ùìù[s] x, P x y)
    (h' : ‚àÄ x y z, x ‚àà s ‚Üí y ‚àà s ‚Üí z ‚àà s ‚Üí P x y ‚Üí P y z ‚Üí P x z)
    (h'' : ‚àÄ x y, x ‚àà s ‚Üí y ‚àà s ‚Üí P x y ‚Üí P y x)
    {x y : Œ±} (hx : x ‚àà s) (hy : y ‚àà s) : P x y := by
  apply hs.induction‚ÇÇ' P (fun z hz ‚Ü¶ ?_) h' hx hy
  filter_upwards [h z hz, self_mem_nhdsWithin] with a ha h'a
  exact ‚ü®ha, h'' z a hz h'a ha‚ü©

/-- A set `s` is preconnected if and only if for every cover by two open sets that are disjoint on
`s`, it is contained in one of the two covering sets. -/
theorem isPreconnected_iff_subset_of_disjoint {s : Set Œ±} :
    IsPreconnected s ‚Üî
      ‚àÄ u v, IsOpen u ‚Üí IsOpen v ‚Üí s ‚äÜ u ‚à™ v ‚Üí s ‚à© (u ‚à© v) = ‚àÖ ‚Üí s ‚äÜ u ‚à® s ‚äÜ v := by
  constructor <;> intro h
  ¬∑ intro u v hu hv hs huv
    specialize h u v hu hv hs
    contrapose! huv
    simp [not_subset] at huv
    rcases huv with ‚ü®‚ü®x, hxs, hxu‚ü©, ‚ü®y, hys, hyv‚ü©‚ü©
    have hxv : x ‚àà v := or_iff_not_imp_left.mp (hs hxs) hxu
    have hyu : y ‚àà u := or_iff_not_imp_right.mp (hs hys) hyv
    exact h ‚ü®y, hys, hyu‚ü© ‚ü®x, hxs, hxv‚ü©
  ¬∑ intro u v hu hv hs hsu hsv
    by_contra H
    specialize h u v hu hv hs (Set.not_nonempty_iff_eq_empty.mp H)
    apply H
    cases' h with h h
    ¬∑ rcases hsv with ‚ü®x, hxs, hxv‚ü©
      exact ‚ü®x, hxs, ‚ü®h hxs, hxv‚ü©‚ü©
    ¬∑ rcases hsu with ‚ü®x, hxs, hxu‚ü©
      exact ‚ü®x, hxs, ‚ü®hxu, h hxs‚ü©‚ü©

/-- A set `s` is connected if and only if
for every cover by a finite collection of open sets that are pairwise disjoint on `s`,
it is contained in one of the members of the collection. -/
theorem isConnected_iff_sUnion_disjoint_open {s : Set Œ±} :
    IsConnected s ‚Üî
      ‚àÄ U : Finset (Set Œ±), (‚àÄ u v : Set Œ±, u ‚àà U ‚Üí v ‚àà U ‚Üí (s ‚à© (u ‚à© v)).Nonempty ‚Üí u = v) ‚Üí
        (‚àÄ u ‚àà U, IsOpen u) ‚Üí (s ‚äÜ ‚ãÉ‚ÇÄ ‚ÜëU) ‚Üí ‚àÉ u ‚àà U, s ‚äÜ u := by
  rw [IsConnected, isPreconnected_iff_subset_of_disjoint]
  refine ‚ü®fun ‚ü®hne, h‚ü© U hU hUo hsU => ?_, fun h => ‚ü®?_, fun u v hu hv hs hsuv => ?_‚ü©‚ü©
  ¬∑ induction U using Finset.induction_on with
    | empty => exact absurd (by simpa using hsU) hne.not_subset_empty
    | @insert u U uU IH =>
      simp only [‚Üê forall_cond_comm, Finset.forall_mem_insert, Finset.exists_mem_insert,
        Finset.coe_insert, sUnion_insert, implies_true, true_and] at *
      refine (h _ hUo.1 (‚ãÉ‚ÇÄ ‚ÜëU) (isOpen_sUnion hUo.2) hsU ?_).imp_right ?_
      ¬∑ refine subset_empty_iff.1 fun x ‚ü®hxs, hxu, v, hvU, hxv‚ü© => ?_
        exact ne_of_mem_of_not_mem hvU uU (hU.1 v hvU ‚ü®x, hxs, hxu, hxv‚ü©).symm
      ¬∑ exact IH (fun u hu => (hU.2 u hu).2) hUo.2
  ¬∑ simpa [subset_empty_iff, nonempty_iff_ne_empty] using h ‚àÖ
  ¬∑ rw [‚Üê not_nonempty_iff_eq_empty] at hsuv
    have := hsuv; rw [inter_comm u] at this
    simpa [*, or_imp, forall_and] using h {u, v}

-- Porting note: `IsPreconnected.subset_isClopen` moved up from here

/-- Preconnected sets are either contained in or disjoint to any given clopen set. -/
theorem disjoint_or_subset_of_isClopen {s t : Set Œ±} (hs : IsPreconnected s) (ht : IsClopen t) :
    Disjoint s t ‚à® s ‚äÜ t :=
  (disjoint_or_nonempty_inter s t).imp_right <| hs.subset_isClopen ht

/-- A set `s` is preconnected if and only if
for every cover by two closed sets that are disjoint on `s`,
it is contained in one of the two covering sets. -/
theorem isPreconnected_iff_subset_of_disjoint_closed :
    IsPreconnected s ‚Üî
      ‚àÄ u v, IsClosed u ‚Üí IsClosed v ‚Üí s ‚äÜ u ‚à™ v ‚Üí s ‚à© (u ‚à© v) = ‚àÖ ‚Üí s ‚äÜ u ‚à® s ‚äÜ v := by
  constructor <;> intro h
  ¬∑ intro u v hu hv hs huv
    rw [isPreconnected_closed_iff] at h
    specialize h u v hu hv hs
    contrapose! huv
    simp [not_subset] at huv
    rcases huv with ‚ü®‚ü®x, hxs, hxu‚ü©, ‚ü®y, hys, hyv‚ü©‚ü©
    have hxv : x ‚àà v := or_iff_not_imp_left.mp (hs hxs) hxu
    have hyu : y ‚àà u := or_iff_not_imp_right.mp (hs hys) hyv
    exact h ‚ü®y, hys, hyu‚ü© ‚ü®x, hxs, hxv‚ü©
  ¬∑ rw [isPreconnected_closed_iff]
    intro u v hu hv hs hsu hsv
    by_contra H
    specialize h u v hu hv hs (Set.not_nonempty_iff_eq_empty.mp H)
    apply H
    cases' h with h h
    ¬∑ rcases hsv with ‚ü®x, hxs, hxv‚ü©
      exact ‚ü®x, hxs, ‚ü®h hxs, hxv‚ü©‚ü©
    ¬∑ rcases hsu with ‚ü®x, hxs, hxu‚ü©
      exact ‚ü®x, hxs, ‚ü®hxu, h hxs‚ü©‚ü©

/-- A closed set `s` is preconnected if and only if for every cover by two closed sets that are
disjoint, it is contained in one of the two covering sets. -/
theorem isPreconnected_iff_subset_of_fully_disjoint_closed {s : Set Œ±} (hs : IsClosed s) :
    IsPreconnected s ‚Üî
      ‚àÄ u v, IsClosed u ‚Üí IsClosed v ‚Üí s ‚äÜ u ‚à™ v ‚Üí Disjoint u v ‚Üí s ‚äÜ u ‚à® s ‚äÜ v := by
  refine isPreconnected_iff_subset_of_disjoint_closed.trans ‚ü®?_, ?_‚ü© <;> intro H u v hu hv hss huv
  ¬∑ apply H u v hu hv hss
    rw [huv.inter_eq, inter_empty]
  have H1 := H (u ‚à© s) (v ‚à© s)
  rw [subset_inter_iff, subset_inter_iff] at H1
  simp only [Subset.refl, and_true] at H1
  apply H1 (hu.inter hs) (hv.inter hs)
  ¬∑ rw [‚Üê union_inter_distrib_right]
    exact subset_inter hss Subset.rfl
  ¬∑ rwa [disjoint_iff_inter_eq_empty, ‚Üê inter_inter_distrib_right, inter_comm]

theorem IsClopen.connectedComponent_subset {x} (hs : IsClopen s) (hx : x ‚àà s) :
    connectedComponent x ‚äÜ s :=
  isPreconnected_connectedComponent.subset_isClopen hs ‚ü®x, mem_connectedComponent, hx‚ü©

/-- The connected component of a point is always a subset of the intersection of all its clopen
neighbourhoods. -/
theorem connectedComponent_subset_iInter_isClopen {x : Œ±} :
    connectedComponent x ‚äÜ ‚ãÇ Z : { Z : Set Œ± // IsClopen Z ‚àß x ‚àà Z }, Z :=
  subset_iInter fun Z => Z.2.1.connectedComponent_subset Z.2.2

/-- A clopen set is the union of its connected components. -/
theorem IsClopen.biUnion_connectedComponent_eq {Z : Set Œ±} (h : IsClopen Z) :
    ‚ãÉ x ‚àà Z, connectedComponent x = Z :=
  Subset.antisymm (iUnion‚ÇÇ_subset fun _ => h.connectedComponent_subset) fun _ h =>
    mem_iUnion‚ÇÇ_of_mem h mem_connectedComponent

/-- The preimage of a connected component is preconnected if the function has connected fibers
and a subset is closed iff the preimage is. -/
theorem preimage_connectedComponent_connected [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤}
    (connected_fibers : ‚àÄ t : Œ≤, IsConnected (f ‚Åª¬π' {t}))
    (hcl : ‚àÄ T : Set Œ≤, IsClosed T ‚Üî IsClosed (f ‚Åª¬π' T)) (t : Œ≤) :
    IsConnected (f ‚Åª¬π' connectedComponent t) := by
  -- The following proof is essentially https://stacks.math.columbia.edu/tag/0377
  -- although the statement is slightly different
  have hf : Surjective f := Surjective.of_comp fun t : Œ≤ => (connected_fibers t).1
  refine ‚ü®Nonempty.preimage connectedComponent_nonempty hf, ?_‚ü©
  have hT : IsClosed (f ‚Åª¬π' connectedComponent t) :=
    (hcl (connectedComponent t)).1 isClosed_connectedComponent
  -- To show it's preconnected we decompose (f ‚Åª¬π' connectedComponent t) as a subset of two
  -- closed disjoint sets in Œ±. We want to show that it's a subset of either.
  rw [isPreconnected_iff_subset_of_fully_disjoint_closed hT]
  intro u v hu hv huv uv_disj
  -- To do this we decompose connectedComponent t into T‚ÇÅ and T‚ÇÇ
  -- we will show that connectedComponent t is a subset of either and hence
  -- (f ‚Åª¬π' connectedComponent t) is a subset of u or v
  let T‚ÇÅ := { t' ‚àà connectedComponent t | f ‚Åª¬π' {t'} ‚äÜ u }
  let T‚ÇÇ := { t' ‚àà connectedComponent t | f ‚Åª¬π' {t'} ‚äÜ v }
  have fiber_decomp : ‚àÄ t' ‚àà connectedComponent t, f ‚Åª¬π' {t'} ‚äÜ u ‚à® f ‚Åª¬π' {t'} ‚äÜ v := by
    intro t' ht'
    apply isPreconnected_iff_subset_of_disjoint_closed.1 (connected_fibers t').2 u v hu hv
    ¬∑ exact Subset.trans (preimage_mono (singleton_subset_iff.2 ht')) huv
    rw [uv_disj.inter_eq, inter_empty]
  have T‚ÇÅ_u : f ‚Åª¬π' T‚ÇÅ = f ‚Åª¬π' connectedComponent t ‚à© u := by
    apply eq_of_subset_of_subset
    ¬∑ rw [‚Üê biUnion_preimage_singleton]
      refine iUnion‚ÇÇ_subset fun t' ht' => subset_inter ?_ ht'.2
      rw [hf.preimage_subset_preimage_iff, singleton_subset_iff]
      exact ht'.1
    rintro a ‚ü®hat, hau‚ü©
    constructor
    ¬∑ exact mem_preimage.1 hat
    refine (fiber_decomp (f a) (mem_preimage.1 hat)).resolve_right fun h => ?_
    exact uv_disj.subset_compl_right hau (h rfl)
  -- This proof is exactly the same as the above (modulo some symmetry)
  have T‚ÇÇ_v : f ‚Åª¬π' T‚ÇÇ = f ‚Åª¬π' connectedComponent t ‚à© v := by
    apply eq_of_subset_of_subset
    ¬∑ rw [‚Üê biUnion_preimage_singleton]
      refine iUnion‚ÇÇ_subset fun t' ht' => subset_inter ?_ ht'.2
      rw [hf.preimage_subset_preimage_iff, singleton_subset_iff]
      exact ht'.1
    rintro a ‚ü®hat, hav‚ü©
    constructor
    ¬∑ exact mem_preimage.1 hat
    ¬∑ refine (fiber_decomp (f a) (mem_preimage.1 hat)).resolve_left fun h => ?_
      exact uv_disj.subset_compl_left hav (h rfl)
  -- Now we show T‚ÇÅ, T‚ÇÇ are closed, cover connectedComponent t and are disjoint.
  have hT‚ÇÅ : IsClosed T‚ÇÅ := (hcl T‚ÇÅ).2 (T‚ÇÅ_u.symm ‚ñ∏ IsClosed.inter hT hu)
  have hT‚ÇÇ : IsClosed T‚ÇÇ := (hcl T‚ÇÇ).2 (T‚ÇÇ_v.symm ‚ñ∏ IsClosed.inter hT hv)
  have T_decomp : connectedComponent t ‚äÜ T‚ÇÅ ‚à™ T‚ÇÇ := fun t' ht' => by
    rw [mem_union t' T‚ÇÅ T‚ÇÇ]
    cases' fiber_decomp t' ht' with htu htv
    ¬∑ left
      exact ‚ü®ht', htu‚ü©
    right
    exact ‚ü®ht', htv‚ü©
  have T_disjoint : Disjoint T‚ÇÅ T‚ÇÇ := by
    refine Disjoint.of_preimage hf ?_
    rw [T‚ÇÅ_u, T‚ÇÇ_v, disjoint_iff_inter_eq_empty, ‚Üê inter_inter_distrib_left, uv_disj.inter_eq,
      inter_empty]
  -- Now we do cases on whether (connectedComponent t) is a subset of T‚ÇÅ or T‚ÇÇ to show
  -- that the preimage is a subset of u or v.
  cases' (isPreconnected_iff_subset_of_fully_disjoint_closed isClosed_connectedComponent).1
    isPreconnected_connectedComponent T‚ÇÅ T‚ÇÇ hT‚ÇÅ hT‚ÇÇ T_decomp T_disjoint with h h
  ¬∑ left
    rw [Subset.antisymm_iff] at T‚ÇÅ_u
    suffices f ‚Åª¬π' connectedComponent t ‚äÜ f ‚Åª¬π' T‚ÇÅ
      from (this.trans T‚ÇÅ_u.1).trans inter_subset_right
    exact preimage_mono h
  ¬∑ right
    rw [Subset.antisymm_iff] at T‚ÇÇ_v
    suffices f ‚Åª¬π' connectedComponent t ‚äÜ f ‚Åª¬π' T‚ÇÇ
      from (this.trans T‚ÇÇ_v.1).trans inter_subset_right
    exact preimage_mono h

theorem QuotientMap.preimage_connectedComponent [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : QuotientMap f) (h_fibers : ‚àÄ y : Œ≤, IsConnected (f ‚Åª¬π' {y})) (a : Œ±) :
    f ‚Åª¬π' connectedComponent (f a) = connectedComponent a :=
  ((preimage_connectedComponent_connected h_fibers (fun _ => hf.isClosed_preimage.symm)
      _).subset_connectedComponent mem_connectedComponent).antisymm
    (hf.continuous.mapsTo_connectedComponent a)

theorem QuotientMap.image_connectedComponent [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤} (hf : QuotientMap f)
    (h_fibers : ‚àÄ y : Œ≤, IsConnected (f ‚Åª¬π' {y})) (a : Œ±) :
    f '' connectedComponent a = connectedComponent (f a) := by
  rw [‚Üê hf.preimage_connectedComponent h_fibers, image_preimage_eq _ hf.surjective]

end Preconnected

section connectedComponentSetoid
/-- The setoid of connected components of a topological space -/
def connectedComponentSetoid (Œ± : Type*) [TopologicalSpace Œ±] : Setoid Œ± :=
  ‚ü®fun x y => connectedComponent x = connectedComponent y,
    ‚ü®fun x => by trivial, fun h1 => h1.symm, fun h1 h2 => h1.trans h2‚ü©‚ü©

/-- The quotient of a space by its connected components -/
def ConnectedComponents (Œ± : Type u) [TopologicalSpace Œ±] :=
  Quotient (connectedComponentSetoid Œ±)

namespace ConnectedComponents

/-- Coercion from a topological space to the set of connected components of this space. -/
def mk : Œ± ‚Üí ConnectedComponents Œ± := Quotient.mk''

instance : CoeTC Œ± (ConnectedComponents Œ±) := ‚ü®mk‚ü©

@[simp]
theorem coe_eq_coe {x y : Œ±} :
    (x : ConnectedComponents Œ±) = y ‚Üî connectedComponent x = connectedComponent y :=
  Quotient.eq''

theorem coe_ne_coe {x y : Œ±} :
    (x : ConnectedComponents Œ±) ‚â† y ‚Üî connectedComponent x ‚â† connectedComponent y :=
  coe_eq_coe.not

theorem coe_eq_coe' {x y : Œ±} : (x : ConnectedComponents Œ±) = y ‚Üî x ‚àà connectedComponent y :=
  coe_eq_coe.trans connectedComponent_eq_iff_mem

instance [Inhabited Œ±] : Inhabited (ConnectedComponents Œ±) :=
  ‚ü®mk default‚ü©

instance : TopologicalSpace (ConnectedComponents Œ±) :=
  inferInstanceAs (TopologicalSpace (Quotient _))

theorem surjective_coe : Surjective (mk : Œ± ‚Üí ConnectedComponents Œ±) :=
  surjective_quot_mk _

theorem quotientMap_coe : QuotientMap (mk : Œ± ‚Üí ConnectedComponents Œ±) :=
  quotientMap_quot_mk

@[continuity]
theorem continuous_coe : Continuous (mk : Œ± ‚Üí ConnectedComponents Œ±) :=
  quotientMap_coe.continuous

@[simp]
theorem range_coe : range (mk : Œ± ‚Üí ConnectedComponents Œ±) = univ :=
  surjective_coe.range_eq

end ConnectedComponents

/-- The preimage of a singleton in `connectedComponents` is the connected component
of an element in the equivalence class. -/
theorem connectedComponents_preimage_singleton {x : Œ±} :
    (‚Üë) ‚Åª¬π' ({‚Üëx} : Set (ConnectedComponents Œ±)) = connectedComponent x := by
  ext y
  rw [mem_preimage, mem_singleton_iff, ConnectedComponents.coe_eq_coe']

/-- The preimage of the image of a set under the quotient map to `connectedComponents Œ±`
is the union of the connected components of the elements in it. -/
theorem connectedComponents_preimage_image (U : Set Œ±) :
    (‚Üë) ‚Åª¬π' ((‚Üë) '' U : Set (ConnectedComponents Œ±)) = ‚ãÉ x ‚àà U, connectedComponent x := by
  simp only [connectedComponents_preimage_singleton, preimage_iUnion‚ÇÇ, image_eq_iUnion]



end connectedComponentSetoid

/-- If every map to `Bool` (a discrete two-element space), that is
continuous on a set `s`, is constant on s, then s is preconnected -/
theorem isPreconnected_of_forall_constant {s : Set Œ±}
    (hs : ‚àÄ f : Œ± ‚Üí Bool, ContinuousOn f s ‚Üí ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, f x = f y) : IsPreconnected s := by
  unfold IsPreconnected
  by_contra!
  rcases this with ‚ü®u, v, u_op, v_op, hsuv, ‚ü®x, x_in_s, x_in_u‚ü©, ‚ü®y, y_in_s, y_in_v‚ü©, H‚ü©
  have hy : y ‚àâ u := fun y_in_u => eq_empty_iff_forall_not_mem.mp H y ‚ü®y_in_s, ‚ü®y_in_u, y_in_v‚ü©‚ü©
  have : ContinuousOn u.boolIndicator s := by
    apply (continuousOn_boolIndicator_iff_isClopen _ _).mpr ‚ü®_, _‚ü©
    ¬∑ rw [preimage_subtype_coe_eq_compl hsuv H]
      exact (v_op.preimage continuous_subtype_val).isClosed_compl
    ¬∑ exact u_op.preimage continuous_subtype_val
  simpa [(u.mem_iff_boolIndicator _).mp x_in_u, (u.not_mem_iff_boolIndicator _).mp hy] using
    hs _ this x x_in_s y y_in_s

/-- A `PreconnectedSpace` version of `isPreconnected_of_forall_constant` -/
theorem preconnectedSpace_of_forall_constant
    (hs : ‚àÄ f : Œ± ‚Üí Bool, Continuous f ‚Üí ‚àÄ x y, f x = f y) : PreconnectedSpace Œ± :=
  ‚ü®isPreconnected_of_forall_constant fun f hf x _ y _ =>
      hs f (continuous_iff_continuousOn_univ.mpr hf) x y‚ü©
