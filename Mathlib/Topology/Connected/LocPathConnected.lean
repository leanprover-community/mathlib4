/-
Copyright (c) 2020 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Ben Eltschig
-/
import Mathlib.Topology.Connected.PathConnected
import Mathlib.Topology.AlexandrovDiscrete

/-!
# Locally path-connected spaces

This file defines `LocPathConnectedSpace X`, a predicate class asserting that `X` is locally
path-connected, in that each point has a basis of path-connected neighborhoods.

## Main results

* `IsOpen.pathComponent` / `IsClosed.pathComponent`: in locally path-connected spaces,
  path-components are both open and closed.
* `pathComponent_eq_connectedComponent`: in locally path-connected spaces, path-components and
  connected components agree.
* `pathConnectedSpace_iff_connectedSpace`: locally path-connected spaces are path-connected iff they
  are connected.
* `instLocallyConnectedSpace`: locally path-connected spaces are also locally connected.
* `IsOpen.locPathConnectedSpace`: open subsets of locally path-connected spaces are
  locally path-connected.
* `LocPathConnectedSpace.coinduced` / `Quotient.locPathConnectedSpace`: quotients of locally
  path-connected spaces are locally path-connected.
* `Sum.locPathConnectedSpace` / `Sigma.locPathConnectedSpace`: disjoint unions of locally
  path-connected spaces are locally path-connected.

Abstractly, this also shows that locally path-connected spaces form a coreflective subcategory of
the category of topological spaces, although we do not prove that in this form here.

## Implementation notes

In the definition of `LocPathConnectedSpace X` we require neighbourhoods in the basis to be
path-connected, but not necessarily open; that they can also be required to be open is shown as
a theorem in `isOpen_isPathConnected_basis`.
-/

noncomputable section

open Topology Filter unitInterval Set Function

variable {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] {x y z : X} {Î¹ : Type*} {F : Set X}

section LocPathConnectedSpace

/-- A topological space is locally path connected, at every point, path connected
neighborhoods form a neighborhood basis. -/
class LocPathConnectedSpace (X : Type*) [TopologicalSpace X] : Prop where
  /-- Each neighborhood filter has a basis of path-connected neighborhoods. -/
  path_connected_basis : âˆ€ x : X, (ğ“ x).HasBasis (fun s : Set X => s âˆˆ ğ“ x âˆ§ IsPathConnected s) id

export LocPathConnectedSpace (path_connected_basis)

theorem LocPathConnectedSpace.of_bases {p : X â†’ Î¹ â†’ Prop} {s : X â†’ Î¹ â†’ Set X}
    (h : âˆ€ x, (ğ“ x).HasBasis (p x) (s x)) (h' : âˆ€ x i, p x i â†’ IsPathConnected (s x i)) :
    LocPathConnectedSpace X where
  path_connected_basis x := by
    rw [hasBasis_self]
    intro t ht
    rcases (h x).mem_iff.mp ht with âŸ¨i, hpi, hiâŸ©
    exact âŸ¨s x i, (h x).mem_of_mem hpi, h' x i hpi, hiâŸ©

variable [LocPathConnectedSpace X]

protected theorem IsOpen.pathComponentIn (hF : IsOpen F) (x : X) :
    IsOpen (pathComponentIn F x) := by
  rw [isOpen_iff_mem_nhds]
  intro y hy
  let âŸ¨s, hsâŸ© := (path_connected_basis y).mem_iff.mp (hF.mem_nhds (pathComponentIn_subset hy))
  exact mem_of_superset hs.1.1 <| pathComponentIn_congr hy â–¸
    hs.1.2.subset_pathComponentIn (mem_of_mem_nhds hs.1.1) hs.2

/-- In a locally path connected space, each path component is an open set. -/
protected theorem IsOpen.pathComponent (x : X) : IsOpen (pathComponent x) := by
  rw [â† pathComponentIn_univ]
  exact isOpen_univ.pathComponentIn _

/-- In a locally path connected space, each path component is a closed set. -/
protected theorem IsClosed.pathComponent (x : X) : IsClosed (pathComponent x) := by
  rw [â† isOpen_compl_iff, isOpen_iff_mem_nhds]
  intro y hxy
  rcases (path_connected_basis y).ex_mem with âŸ¨V, hVy, hVcâŸ©
  filter_upwards [hVy] with z hz hxz
  exact hxy <|  hxz.trans (hVc.joinedIn _ hz _ (mem_of_mem_nhds hVy)).joined

/-- In a locally path connected space, each path component is a clopen set. -/
protected theorem IsClopen.pathComponent (x : X) : IsClopen (pathComponent x) :=
  âŸ¨.pathComponent x, .pathComponent xâŸ©

lemma pathComponentIn_mem_nhds (hF : F âˆˆ ğ“ x) : pathComponentIn F x âˆˆ ğ“ x := by
  let âŸ¨u, huF, hu, hxuâŸ© := mem_nhds_iff.mp hF
  exact mem_nhds_iff.mpr âŸ¨pathComponentIn u x, pathComponentIn_mono huF,
    hu.pathComponentIn x, mem_pathComponentIn_self hxuâŸ©

theorem pathConnectedSpace_iff_connectedSpace : PathConnectedSpace X â†” ConnectedSpace X := by
  refine âŸ¨fun _ â†¦ inferInstance, fun h â†¦ âŸ¨inferInstance, fun x y â†¦ ?_âŸ©âŸ©
  rw [â† mem_pathComponent_iff, (IsClopen.pathComponent _).eq_univ] <;> simp

theorem pathComponent_eq_connectedComponent (x : X) : pathComponent x = connectedComponent x :=
  (pathComponent_subset_component x).antisymm <|
    (IsClopen.pathComponent x).connectedComponent_subset (mem_pathComponent_self _)

theorem pathConnected_subset_basis {U : Set X} (h : IsOpen U) (hx : x âˆˆ U) :
    (ğ“ x).HasBasis (fun s : Set X => s âˆˆ ğ“ x âˆ§ IsPathConnected s âˆ§ s âŠ† U) id :=
  (path_connected_basis x).hasBasis_self_subset (IsOpen.mem_nhds h hx)

theorem isOpen_isPathConnected_basis (x : X) :
    (ğ“ x).HasBasis (fun s : Set X â†¦ IsOpen s âˆ§ x âˆˆ s âˆ§ IsPathConnected s) id := by
  refine âŸ¨fun s â†¦ âŸ¨fun hs â†¦ ?_, fun âŸ¨u, huâŸ© â†¦ mem_nhds_iff.mpr âŸ¨u, hu.2, hu.1.1, hu.1.2.1âŸ©âŸ©âŸ©
  have âŸ¨u, hus, hu, hxuâŸ© := mem_nhds_iff.mp hs
  exact âŸ¨pathComponentIn u x, âŸ¨hu.pathComponentIn _, âŸ¨mem_pathComponentIn_self hxu,
    isPathConnected_pathComponentIn hxuâŸ©âŸ©, pathComponentIn_subset.trans husâŸ©

theorem Topology.IsOpenEmbedding.locPathConnectedSpace {e : Y â†’ X} (he : IsOpenEmbedding e) :
    LocPathConnectedSpace Y :=
  have (y : Y) :
      (ğ“ y).HasBasis (fun s â†¦ s âˆˆ ğ“ (e y) âˆ§ IsPathConnected s âˆ§ s âŠ† range e) (e â»Â¹' Â·) :=
    he.basis_nhds <| pathConnected_subset_basis he.isOpen_range (mem_range_self _)
  .of_bases this fun x s âŸ¨_, hs, hseâŸ© â†¦ by
    rwa [he.isPathConnected_iff, image_preimage_eq_of_subset hse]

theorem IsOpen.locPathConnectedSpace {U : Set X} (h : IsOpen U) : LocPathConnectedSpace U :=
  h.isOpenEmbedding_subtypeVal.locPathConnectedSpace

theorem IsOpen.isConnected_iff_isPathConnected {U : Set X} (U_op : IsOpen U) :
    IsConnected U â†” IsPathConnected U := by
  rw [isConnected_iff_connectedSpace, isPathConnected_iff_pathConnectedSpace]
  haveI := U_op.locPathConnectedSpace
  exact pathConnectedSpace_iff_connectedSpace.symm

/-- Locally path-connected spaces are locally connected. -/
instance : LocallyConnectedSpace X := by
  refine âŸ¨forall_imp (fun x h â†¦ âŸ¨fun s â†¦ ?_âŸ©) isOpen_isPathConnected_basisâŸ©
  refine âŸ¨fun hs â†¦ ?_, fun âŸ¨u, âŸ¨hu, hxu, _âŸ©, husâŸ© â†¦ mem_nhds_iff.mpr âŸ¨u, hus, hu, hxuâŸ©âŸ©
  let âŸ¨u, âŸ¨hu, hxu, hu'âŸ©, husâŸ© := (h.mem_iff' s).mp hs
  exact âŸ¨u, âŸ¨hu, hxu, hu'.isConnectedâŸ©, husâŸ©

/-- A space is locally path-connected iff all path components of open subsets are open. -/
lemma locPathConnectedSpace_iff_isOpen_pathComponentIn {X : Type*} [TopologicalSpace X] :
    LocPathConnectedSpace X â†” âˆ€ (x : X) (u : Set X), IsOpen u â†’ IsOpen (pathComponentIn u x) :=
  âŸ¨fun _ _ _ hu â†¦ hu.pathComponentIn _, fun h â†¦ âŸ¨fun x â†¦ âŸ¨fun s â†¦ by
    refine âŸ¨fun hs â†¦ ?_, fun âŸ¨_, htâŸ© â†¦ Filter.mem_of_superset ht.1.1 ht.2âŸ©
    let âŸ¨u, huâŸ© := mem_nhds_iff.mp hs
    exact âŸ¨pathComponentIn u x, âŸ¨(h x u hu.2.1).mem_nhds (mem_pathComponentIn_self hu.2.2),
      isPathConnected_pathComponentIn hu.2.2âŸ©, pathComponentIn_subset.trans hu.1âŸ©âŸ©âŸ©âŸ©

/-- A space is locally path-connected iff all path components of open subsets are neighbourhoods. -/
lemma locPathConnectedSpace_iff_pathComponentIn_mem_nhds {X : Type*} [TopologicalSpace X] :
    LocPathConnectedSpace X â†”
    âˆ€ x : X, âˆ€ u : Set X, IsOpen u â†’ x âˆˆ u â†’ pathComponentIn u x âˆˆ nhds x := by
  rw [locPathConnectedSpace_iff_isOpen_pathComponentIn]
  simp_rw [forall_comm (Î² := Set X), â† imp_forall_iff]
  refine forall_congr' fun u â†¦ imp_congr_right fun _ â†¦ ?_
  exact âŸ¨fun h x hxu â†¦ (h x).mem_nhds (mem_pathComponentIn_self hxu),
    fun h x â†¦ isOpen_iff_mem_nhds.mpr fun y hy â†¦
      pathComponentIn_congr hy â–¸ h y <| pathComponentIn_subset hyâŸ©

/-- Any topology coinduced by a locally path-connected topology is locally path-connected. -/
lemma LocPathConnectedSpace.coinduced {Y : Type*} (f : X â†’ Y) :
    @LocPathConnectedSpace Y (.coinduced f â€¹_â€º) := by
  let _ := TopologicalSpace.coinduced f â€¹_â€º; have hf : Continuous f := continuous_coinduced_rng
  refine locPathConnectedSpace_iff_isOpen_pathComponentIn.mpr fun y u hu â†¦
    isOpen_coinduced.mpr <| isOpen_iff_mem_nhds.mpr fun x hx â†¦ ?_
  have hx' := preimage_mono pathComponentIn_subset hx
  refine mem_nhds_iff.mpr âŸ¨pathComponentIn (f â»Â¹' u) x, ?_,
    (hu.preimage hf).pathComponentIn _, mem_pathComponentIn_self hx'âŸ©
  rw [â† image_subset_iff, â† pathComponentIn_congr hx]
  exact ((isPathConnected_pathComponentIn hx').image hf).subset_pathComponentIn
    âŸ¨x, mem_pathComponentIn_self hx', rflâŸ© <|
    (image_mono pathComponentIn_subset).trans <| u.image_preimage_subset f

/-- Quotients of locally path-connected spaces are locally path-connected. -/
lemma Topology.IsQuotientMap.locPathConnectedSpace {f : X â†’ Y} (h : IsQuotientMap f) :
    LocPathConnectedSpace Y :=
  h.2 â–¸ LocPathConnectedSpace.coinduced f

/-- Quotients of locally path-connected spaces are locally path-connected. -/
instance Quot.locPathConnectedSpace {r : X â†’ X â†’ Prop} : LocPathConnectedSpace (Quot r) :=
  isQuotientMap_quot_mk.locPathConnectedSpace

/-- Quotients of locally path-connected spaces are locally path-connected. -/
instance Quotient.locPathConnectedSpace {s : Setoid X} : LocPathConnectedSpace (Quotient s) :=
  isQuotientMap_quotient_mk'.locPathConnectedSpace

/-- Disjoint unions of locally path-connected spaces are locally path-connected. -/
instance Sum.locPathConnectedSpace.{u} {X Y : Type u} [TopologicalSpace X] [TopologicalSpace Y]
    [LocPathConnectedSpace X] [LocPathConnectedSpace Y] :
    LocPathConnectedSpace (X âŠ• Y) := by
  rw [locPathConnectedSpace_iff_pathComponentIn_mem_nhds]; intro x u hu hxu; rw [mem_nhds_iff]
  obtain x | y := x
  Â· refine âŸ¨Sum.inl '' (pathComponentIn (Sum.inl â»Â¹' u) x), ?_, ?_, ?_âŸ©
    Â· apply IsPathConnected.subset_pathComponentIn
      Â· exact (isPathConnected_pathComponentIn (by exact hxu)).image continuous_inl
      Â· exact âŸ¨x, mem_pathComponentIn_self hxu, rflâŸ©
      Â· exact (image_mono pathComponentIn_subset).trans (u.image_preimage_subset _)
    Â· exact isOpenMap_inl _ <| (hu.preimage continuous_inl).pathComponentIn _
    Â· exact âŸ¨x, mem_pathComponentIn_self hxu, rflâŸ©
  Â· refine âŸ¨Sum.inr '' (pathComponentIn (Sum.inr â»Â¹' u) y), ?_, ?_, ?_âŸ©
    Â· apply IsPathConnected.subset_pathComponentIn
      Â· exact (isPathConnected_pathComponentIn (by exact hxu)).image continuous_inr
      Â· exact âŸ¨y, mem_pathComponentIn_self hxu, rflâŸ©
      Â· exact (image_mono pathComponentIn_subset).trans (u.image_preimage_subset _)
    Â· exact isOpenMap_inr _ <| (hu.preimage continuous_inr).pathComponentIn _
    Â· exact âŸ¨y, mem_pathComponentIn_self hxu, rflâŸ©

/-- Disjoint unions of locally path-connected spaces are locally path-connected. -/
instance Sigma.locPathConnectedSpace {X : Î¹ â†’ Type*}
    [(i : Î¹) â†’ TopologicalSpace (X i)] [(i : Î¹) â†’ LocPathConnectedSpace (X i)] :
    LocPathConnectedSpace ((i : Î¹) Ã— X i) := by
  rw [locPathConnectedSpace_iff_pathComponentIn_mem_nhds]; intro x u hu hxu; rw [mem_nhds_iff]
  refine âŸ¨(Sigma.mk x.1) '' (pathComponentIn ((Sigma.mk x.1) â»Â¹' u) x.2), ?_, ?_, ?_âŸ©
  Â· apply IsPathConnected.subset_pathComponentIn
    Â· exact (isPathConnected_pathComponentIn (by exact hxu)).image continuous_sigmaMk
    Â· exact âŸ¨x.2, mem_pathComponentIn_self hxu, rflâŸ©
    Â· exact (image_mono pathComponentIn_subset).trans (u.image_preimage_subset _)
  Â· exact isOpenMap_sigmaMk _ <| (hu.preimage continuous_sigmaMk).pathComponentIn _
  Â· exact âŸ¨x.2, mem_pathComponentIn_self hxu, rflâŸ©

instance AlexandrovDiscrete.locPathConnectedSpace [AlexandrovDiscrete X] :
    LocPathConnectedSpace X := by
  apply LocPathConnectedSpace.of_bases nhds_basis_nhdsKer_singleton
  simp only [forall_const, IsPathConnected, mem_nhdsKer_singleton]
  intro x
  exists x, specializes_rfl
  intro y hy
  symm
  apply hy.joinedIn <;> rewrite [mem_nhdsKer_singleton] <;> [assumption; rfl]

end LocPathConnectedSpace
