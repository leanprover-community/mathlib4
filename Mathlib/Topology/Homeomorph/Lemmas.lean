/-
Copyright (c) 2019 Reid Barton. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Patrick Massot, S√©bastien Gou√´zel, Zhouhang Zhou, Reid Barton
-/
import Mathlib.Logic.Equiv.Fin.Basic
import Mathlib.Topology.Connected.LocallyConnected
import Mathlib.Topology.DenseEmbedding
import Mathlib.Topology.Connected.TotallyDisconnected

/-!
# Further properties of homeomorphisms

This file proves further properties of homeomorphisms between topological spaces.
Pretty much every topological property is preserved under homeomorphisms.

-/

assert_not_exists Module MonoidWithZero

open Filter Function Set Topology

variable {X Y W Z : Type*}

section

variable [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace W] [TopologicalSpace Z]
  {X' Y' : Type*} [TopologicalSpace X'] [TopologicalSpace Y']

namespace Homeomorph

protected theorem secondCountableTopology [SecondCountableTopology Y]
    (h : X ‚âÉ‚Çú Y) : SecondCountableTopology X :=
  h.isInducing.secondCountableTopology

/-- If `h : X ‚Üí Y` is a homeomorphism, `h(s)` is compact iff `s` is. -/
@[simp]
theorem isCompact_image {s : Set X} (h : X ‚âÉ‚Çú Y) : IsCompact (h '' s) ‚Üî IsCompact s :=
  h.isEmbedding.isCompact_iff.symm

/-- If `h : X ‚Üí Y` is a homeomorphism, `h‚Åª¬π(s)` is compact iff `s` is. -/
@[simp]
theorem isCompact_preimage {s : Set Y} (h : X ‚âÉ‚Çú Y) : IsCompact (h ‚Åª¬π' s) ‚Üî IsCompact s := by
  rw [‚Üê image_symm]; exact h.symm.isCompact_image

/-- If `h : X ‚Üí Y` is a homeomorphism, `s` is œÉ-compact iff `h(s)` is. -/
@[simp]
theorem isSigmaCompact_image {s : Set X} (h : X ‚âÉ‚Çú Y) :
    IsSigmaCompact (h '' s) ‚Üî IsSigmaCompact s :=
  h.isEmbedding.isSigmaCompact_iff.symm

/-- If `h : X ‚Üí Y` is a homeomorphism, `h‚Åª¬π(s)` is œÉ-compact iff `s` is. -/
@[simp]
theorem isSigmaCompact_preimage {s : Set Y} (h : X ‚âÉ‚Çú Y) :
    IsSigmaCompact (h ‚Åª¬π' s) ‚Üî IsSigmaCompact s := by
  rw [‚Üê image_symm]; exact h.symm.isSigmaCompact_image

@[simp]
theorem isPreconnected_image {s : Set X} (h : X ‚âÉ‚Çú Y) :
    IsPreconnected (h '' s) ‚Üî IsPreconnected s :=
  ‚ü®fun hs ‚Ü¶ by simpa only [image_symm, preimage_image]
    using hs.image _ h.symm.continuous.continuousOn,
    fun hs ‚Ü¶ hs.image _ h.continuous.continuousOn‚ü©

@[simp]
theorem isPreconnected_preimage {s : Set Y} (h : X ‚âÉ‚Çú Y) :
    IsPreconnected (h ‚Åª¬π' s) ‚Üî IsPreconnected s := by
  rw [‚Üê image_symm, isPreconnected_image]

@[simp]
theorem isConnected_image {s : Set X} (h : X ‚âÉ‚Çú Y) :
    IsConnected (h '' s) ‚Üî IsConnected s :=
  image_nonempty.and h.isPreconnected_image

@[simp]
theorem isConnected_preimage {s : Set Y} (h : X ‚âÉ‚Çú Y) :
    IsConnected (h ‚Åª¬π' s) ‚Üî IsConnected s := by
  rw [‚Üê image_symm, isConnected_image]

theorem image_connectedComponentIn {s : Set X} (h : X ‚âÉ‚Çú Y) {x : X} (hx : x ‚àà s) :
    h '' connectedComponentIn s x = connectedComponentIn (h '' s) (h x) := by
  refine (h.continuous.image_connectedComponentIn_subset hx).antisymm ?_
  have := h.symm.continuous.image_connectedComponentIn_subset (mem_image_of_mem h hx)
  rwa [image_subset_iff, h.preimage_symm, h.image_symm, h.preimage_image, h.symm_apply_apply]
    at this

@[simp]
theorem comap_cocompact (h : X ‚âÉ‚Çú Y) : comap h (cocompact Y) = cocompact X :=
  (comap_cocompact_le h.continuous).antisymm <|
    (hasBasis_cocompact.le_basis_iff (hasBasis_cocompact.comap h)).2 fun K hK =>
      ‚ü®h ‚Åª¬π' K, h.isCompact_preimage.2 hK, Subset.rfl‚ü©

@[simp]
theorem map_cocompact (h : X ‚âÉ‚Çú Y) : map h (cocompact X) = cocompact Y := by
  rw [‚Üê h.comap_cocompact, map_comap_of_surjective h.surjective]

protected theorem compactSpace [CompactSpace X] (h : X ‚âÉ‚Çú Y) : CompactSpace Y where
  isCompact_univ := h.symm.isCompact_preimage.2 isCompact_univ

theorem isDenseEmbedding (h : X ‚âÉ‚Çú Y) : IsDenseEmbedding h :=
  { h.isEmbedding with dense := h.surjective.denseRange }

protected lemma totallyDisconnectedSpace (h : X ‚âÉ‚Çú Y) [tdc : TotallyDisconnectedSpace X] :
    TotallyDisconnectedSpace Y :=
  (totallyDisconnectedSpace_iff Y).mpr
    (h.range_coe ‚ñ∏ ((IsEmbedding.isTotallyDisconnected_range h.isEmbedding).mpr tdc))

@[simp]
theorem map_punctured_nhds_eq (h : X ‚âÉ‚Çú Y) (x : X) : map h (ùìù[‚â†] x) = ùìù[‚â†] (h x) := by
  convert h.isEmbedding.map_nhdsWithin_eq ({x}·∂ú) x
  rw [h.image_compl, Set.image_singleton]

@[simp]
theorem comap_coclosedCompact (h : X ‚âÉ‚Çú Y) : comap h (coclosedCompact Y) = coclosedCompact X :=
  (hasBasis_coclosedCompact.comap h).eq_of_same_basis <| by
    simpa [comp_def] using hasBasis_coclosedCompact.comp_surjective h.injective.preimage_surjective

@[simp]
theorem map_coclosedCompact (h : X ‚âÉ‚Çú Y) : map h (coclosedCompact X) = coclosedCompact Y := by
  rw [‚Üê h.comap_coclosedCompact, map_comap_of_surjective h.surjective]

/-- If the codomain of a homeomorphism is a locally connected space, then the domain is also
a locally connected space. -/
theorem locallyConnectedSpace [i : LocallyConnectedSpace Y] (h : X ‚âÉ‚Çú Y) :
    LocallyConnectedSpace X := by
  have : ‚àÄ x, (ùìù x).HasBasis (fun s ‚Ü¶ IsOpen s ‚àß h x ‚àà s ‚àß IsConnected s)
      (h.symm '' ¬∑) := fun x ‚Ü¶ by
    rw [‚Üê h.symm_map_nhds_eq]
    exact (i.1 _).map _
  refine locallyConnectedSpace_of_connected_bases _ _ this fun _ _ hs ‚Ü¶ ?_
  exact hs.2.2.2.image _ h.symm.continuous.continuousOn

/-- The codomain of a homeomorphism is a locally compact space if and only if
the domain is a locally compact space. -/
theorem locallyCompactSpace_iff (h : X ‚âÉ‚Çú Y) :
    LocallyCompactSpace X ‚Üî LocallyCompactSpace Y := by
  exact ‚ü®fun _ => h.symm.isOpenEmbedding.locallyCompactSpace,
    fun _ => h.isClosedEmbedding.locallyCompactSpace‚ü©

@[simp]
theorem comp_continuousOn_iff (h : X ‚âÉ‚Çú Y) (f : Z ‚Üí X) (s : Set Z) :
    ContinuousOn (h ‚àò f) s ‚Üî ContinuousOn f s :=
  h.isInducing.continuousOn_iff.symm

theorem comp_continuousWithinAt_iff (h : X ‚âÉ‚Çú Y) (f : Z ‚Üí X) (s : Set Z) (z : Z) :
    ContinuousWithinAt f s z ‚Üî ContinuousWithinAt (h ‚àò f) s z :=
  h.isInducing.continuousWithinAt_iff

/-- A homeomorphism `h : X ‚âÉ‚Çú Y` lifts to a homeomorphism between subtypes corresponding to
predicates `p : X ‚Üí Prop` and `q : Y ‚Üí Prop` so long as `p = q ‚àò h`. -/
@[simps!]
def subtype {p : X ‚Üí Prop} {q : Y ‚Üí Prop} (h : X ‚âÉ‚Çú Y) (h_iff : ‚àÄ x, p x ‚Üî q (h x)) :
    {x // p x} ‚âÉ‚Çú {y // q y} where
  continuous_toFun := by simpa [Equiv.coe_subtypeEquiv_eq_map] using h.continuous.subtype_map _
  continuous_invFun := by simpa [Equiv.coe_subtypeEquiv_eq_map] using
    h.symm.continuous.subtype_map _
  __ := h.subtypeEquiv h_iff

@[simp]
lemma subtype_toEquiv {p : X ‚Üí Prop} {q : Y ‚Üí Prop} (h : X ‚âÉ‚Çú Y) (h_iff : ‚àÄ x, p x ‚Üî q (h x)) :
    (h.subtype h_iff).toEquiv = h.toEquiv.subtypeEquiv h_iff :=
  rfl

/-- A homeomorphism `h : X ‚âÉ‚Çú Y` lifts to a homeomorphism between sets `s : Set X` and `t : Set Y`
whenever `h` maps `s` onto `t`. -/
abbrev sets {s : Set X} {t : Set Y} (h : X ‚âÉ‚Çú Y) (h_eq : s = h ‚Åª¬π' t) : s ‚âÉ‚Çú t :=
  h.subtype <| Set.ext_iff.mp h_eq

/-- If two sets are equal, then they are homeomorphic. -/
def setCongr {s t : Set X} (h : s = t) : s ‚âÉ‚Çú t where
  continuous_toFun := continuous_inclusion h.subset
  continuous_invFun := continuous_inclusion h.symm.subset
  toEquiv := Equiv.setCongr h

section prod

variable (X Y W Z)

/-- `X √ó {*}` is homeomorphic to `X`. -/
@[simps! symm_apply_snd]
def prodUnique [Unique Y] :
    X √ó Y ‚âÉ‚Çú X where
  toEquiv := Equiv.prodUnique X Y
  continuous_toFun := continuous_fst
  continuous_invFun := continuous_id.prodMk continuous_const

@[simp] theorem coe_prodUnique [Unique Y] : ‚áë(prodUnique X Y) = Prod.fst := rfl

/-- `X √ó {*}` is homeomorphic to `X`. -/
@[simps! symm_apply_snd]
def uniqueProd (X Y : Type*) [TopologicalSpace X] [TopologicalSpace Y] [Unique X] :
    X √ó Y ‚âÉ‚Çú Y :=
  (prodComm _ _).trans (prodUnique Y X)

@[simp] theorem coe_uniqueProd [Unique X] : ‚áë(uniqueProd X Y) = Prod.snd := rfl

/-- The product over `S ‚äï T` of a family of topological spaces
is homeomorphic to the product of (the product over `S`) and (the product over `T`).

This is `Equiv.sumPiEquivProdPi` as a `Homeomorph`.
-/
def sumPiEquivProdPi (S T : Type*) (A : S ‚äï T ‚Üí Type*)
    [‚àÄ st, TopologicalSpace (A st)] :
    (Œ† (st : S ‚äï T), A st) ‚âÉ‚Çú (Œ† (s : S), A (.inl s)) √ó (Œ† (t : T), A (.inr t)) where
  __ := Equiv.sumPiEquivProdPi _
  continuous_toFun := .prodMk (by fun_prop) (by fun_prop)
  continuous_invFun := continuous_pi <| by rintro (s | t) <;> simp <;> fun_prop

/-- The product `Œ† t : Œ±, f t` of a family of topological spaces is homeomorphic to the
space `f ‚¨ù` when `Œ±` only contains `‚¨ù`.

This is `Equiv.piUnique` as a `Homeomorph`.
-/
@[simps! -fullyApplied]
def piUnique {Œ± : Type*} [Unique Œ±] (f : Œ± ‚Üí Type*) [‚àÄ x, TopologicalSpace (f x)] :
    (Œ† t, f t) ‚âÉ‚Çú f default :=
  (Equiv.piUnique f).toHomeomorphOfContinuousOpen (continuous_apply default) (isOpenMap_eval _)

end prod

/-- `Equiv.piCongrLeft` as a homeomorphism: this is the natural homeomorphism
`Œ† i, Y (e i) ‚âÉ‚Çú Œ† j, Y j` obtained from a bijection `Œπ ‚âÉ Œπ'`. -/
@[simps +simpRhs toEquiv, simps! -isSimp apply]
def piCongrLeft {Œπ Œπ' : Type*} {Y : Œπ' ‚Üí Type*} [‚àÄ j, TopologicalSpace (Y j)]
    (e : Œπ ‚âÉ Œπ') : (‚àÄ i, Y (e i)) ‚âÉ‚Çú ‚àÄ j, Y j where
  continuous_toFun := continuous_pi <| e.forall_congr_right.mp fun i ‚Ü¶ by
    simpa only [Equiv.toFun_as_coe, Equiv.piCongrLeft_apply_apply] using continuous_apply i
  continuous_invFun := Pi.continuous_precomp' e
  toEquiv := Equiv.piCongrLeft _ e

@[simp]
lemma piCongrLeft_refl {Œπ : Type*} {X : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (X i)] :
    piCongrLeft (.refl Œπ) = .refl (‚àÄ i, X i) :=
  rfl

@[simp]
lemma piCongrLeft_symm_apply {Œπ Œπ' : Type*} {Y : Œπ' ‚Üí Type*} [‚àÄ j, TopologicalSpace (Y j)]
    (e : Œπ ‚âÉ Œπ') : ‚áë(piCongrLeft (Y := Y) e).symm = (¬∑ <| e ¬∑) :=
  rfl

@[simp]
lemma piCongrLeft_apply_apply {Œπ Œπ' : Type*} {Y : Œπ' ‚Üí Type*} [‚àÄ j, TopologicalSpace (Y j)]
    (e : Œπ ‚âÉ Œπ') (x : ‚àÄ i, Y (e i)) (i : Œπ) : piCongrLeft e x (e i) = x i :=
  Equiv.piCongrLeft_apply_apply ..

/-- `Equiv.piCongrRight` as a homeomorphism: this is the natural homeomorphism
`Œ† i, Y‚ÇÅ i ‚âÉ‚Çú Œ† j, Y‚ÇÇ i` obtained from homeomorphisms `Y‚ÇÅ i ‚âÉ‚Çú Y‚ÇÇ i` for each `i`. -/
@[simps! apply toEquiv]
def piCongrRight {Œπ : Type*} {Y‚ÇÅ Y‚ÇÇ : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (Y‚ÇÅ i)]
    [‚àÄ i, TopologicalSpace (Y‚ÇÇ i)] (F : ‚àÄ i, Y‚ÇÅ i ‚âÉ‚Çú Y‚ÇÇ i) : (‚àÄ i, Y‚ÇÅ i) ‚âÉ‚Çú ‚àÄ i, Y‚ÇÇ i where
  continuous_toFun := Pi.continuous_postcomp' fun i ‚Ü¶ (F i).continuous
  continuous_invFun := Pi.continuous_postcomp' fun i ‚Ü¶ (F i).symm.continuous
  toEquiv := Equiv.piCongrRight fun i => (F i).toEquiv

@[simp]
theorem piCongrRight_symm {Œπ : Type*} {Y‚ÇÅ Y‚ÇÇ : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (Y‚ÇÅ i)]
    [‚àÄ i, TopologicalSpace (Y‚ÇÇ i)] (F : ‚àÄ i, Y‚ÇÅ i ‚âÉ‚Çú Y‚ÇÇ i) :
    (piCongrRight F).symm = piCongrRight fun i => (F i).symm :=
  rfl

/-- `Equiv.piCongr` as a homeomorphism: this is the natural homeomorphism
`Œ† i‚ÇÅ, Y‚ÇÅ i ‚âÉ‚Çú Œ† i‚ÇÇ, Y‚ÇÇ i‚ÇÇ` obtained from a bijection `Œπ‚ÇÅ ‚âÉ Œπ‚ÇÇ` and homeomorphisms
`Y‚ÇÅ i‚ÇÅ ‚âÉ‚Çú Y‚ÇÇ (e i‚ÇÅ)` for each `i‚ÇÅ : Œπ‚ÇÅ`. -/
@[simps! apply toEquiv]
def piCongr {Œπ‚ÇÅ Œπ‚ÇÇ : Type*} {Y‚ÇÅ : Œπ‚ÇÅ ‚Üí Type*} {Y‚ÇÇ : Œπ‚ÇÇ ‚Üí Type*}
    [‚àÄ i‚ÇÅ, TopologicalSpace (Y‚ÇÅ i‚ÇÅ)] [‚àÄ i‚ÇÇ, TopologicalSpace (Y‚ÇÇ i‚ÇÇ)]
    (e : Œπ‚ÇÅ ‚âÉ Œπ‚ÇÇ) (F : ‚àÄ i‚ÇÅ, Y‚ÇÅ i‚ÇÅ ‚âÉ‚Çú Y‚ÇÇ (e i‚ÇÅ)) : (‚àÄ i‚ÇÅ, Y‚ÇÅ i‚ÇÅ) ‚âÉ‚Çú ‚àÄ i‚ÇÇ, Y‚ÇÇ i‚ÇÇ :=
  (Homeomorph.piCongrRight F).trans (Homeomorph.piCongrLeft e)

/-- `ULift X` is homeomorphic to `X`. -/
def ulift.{u, v} {X : Type v} [TopologicalSpace X] : ULift.{u, v} X ‚âÉ‚Çú X where
  continuous_toFun := continuous_uliftDown
  continuous_invFun := continuous_uliftUp
  toEquiv := Equiv.ulift

/-- The natural homeomorphism `(Œπ ‚äï Œπ' ‚Üí X) ‚âÉ‚Çú (Œπ ‚Üí X) √ó (Œπ' ‚Üí X)`.
`Equiv.sumArrowEquivProdArrow` as a homeomorphism. -/
@[simps!]
def sumArrowHomeomorphProdArrow {Œπ Œπ' : Type*} : (Œπ ‚äï Œπ' ‚Üí X) ‚âÉ‚Çú (Œπ ‚Üí X) √ó (Œπ' ‚Üí X)  where
  toEquiv := Equiv.sumArrowEquivProdArrow _ _ _
  continuous_toFun := by
    dsimp [Equiv.sumArrowEquivProdArrow]
    fun_prop
  continuous_invFun := continuous_pi fun i ‚Ü¶ match i with
    | .inl i => by apply (continuous_apply _).comp' continuous_fst
    | .inr i => by apply (continuous_apply _).comp' continuous_snd

private theorem _root_.Fin.appendEquiv_eq_homeomorph (m n : ‚Ñï) : Fin.appendEquiv m n =
    ((sumArrowHomeomorphProdArrow).symm.trans
    (piCongrLeft (Y := fun _ ‚Ü¶ X) finSumFinEquiv)).toEquiv := by
  apply Equiv.symm_bijective.injective
  ext x i <;> simp

theorem _root_.Fin.continuous_append (m n : ‚Ñï) :
    Continuous fun (p : (Fin m ‚Üí X) √ó (Fin n ‚Üí X)) ‚Ü¶ Fin.append p.1 p.2 := by
  suffices Continuous (Fin.appendEquiv m n) by exact this
  rw [Fin.appendEquiv_eq_homeomorph]
  exact Homeomorph.continuous_toFun _

/-- The natural homeomorphism between `(Fin m ‚Üí X) √ó (Fin n ‚Üí X)` and `Fin (m + n) ‚Üí X`.
`Fin.appendEquiv` as a homeomorphism -/
@[simps!]
def _root_.Fin.appendHomeomorph (m n : ‚Ñï) : (Fin m ‚Üí X) √ó (Fin n ‚Üí X) ‚âÉ‚Çú (Fin (m + n) ‚Üí X) where
  toEquiv := Fin.appendEquiv m n
  continuous_toFun := Fin.continuous_append m n
  continuous_invFun := by
    rw [Fin.appendEquiv_eq_homeomorph]
    exact Homeomorph.continuous_invFun _

@[simp]
theorem _root_.Fin.appendHomeomorph_toEquiv (m n : ‚Ñï) :
    (Fin.appendHomeomorph (X := X) m n).toEquiv = Fin.appendEquiv m n :=
  rfl

section Distrib

variable {Œπ : Type*} {X : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (X i)]

/-- `(Œ£ i, X i) √ó Y` is homeomorphic to `Œ£ i, (X i √ó Y)`. -/
@[simps! apply symm_apply toEquiv]
def sigmaProdDistrib : (Œ£ i, X i) √ó Y ‚âÉ‚Çú Œ£ i, X i √ó Y :=
  Homeomorph.symm <|
    (Equiv.sigmaProdDistrib X Y).symm.toHomeomorphOfContinuousOpen
      (continuous_sigma fun _ => continuous_sigmaMk.fst'.prodMk continuous_snd)
      (isOpenMap_sigma.2 fun _ => isOpenMap_sigmaMk.prodMap IsOpenMap.id)

end Distrib

/-- If `Œπ` has a unique element, then `Œπ ‚Üí X` is homeomorphic to `X`. -/
@[simps! -fullyApplied]
def funUnique (Œπ X : Type*) [Unique Œπ] [TopologicalSpace X] : (Œπ ‚Üí X) ‚âÉ‚Çú X where
  toEquiv := Equiv.funUnique Œπ X
  continuous_toFun := continuous_apply _
  continuous_invFun := continuous_pi fun _ => continuous_id

/-- Homeomorphism between dependent functions `Œ† i : Fin 2, X i` and `X 0 √ó X 1`. -/
@[simps! -fullyApplied]
def piFinTwo.{u} (X : Fin 2 ‚Üí Type u) [‚àÄ i, TopologicalSpace (X i)] : (‚àÄ i, X i) ‚âÉ‚Çú X 0 √ó X 1 where
  toEquiv := piFinTwoEquiv X
  continuous_toFun := (continuous_apply 0).prodMk (continuous_apply 1)
  continuous_invFun := continuous_pi <| Fin.forall_fin_two.2 ‚ü®continuous_fst, continuous_snd‚ü©

/-- Homeomorphism between `X¬≤ = Fin 2 ‚Üí X` and `X √ó X`. -/
@[simps! -fullyApplied]
def finTwoArrow : (Fin 2 ‚Üí X) ‚âÉ‚Çú X √ó X :=
  { piFinTwo fun _ => X with toEquiv := finTwoArrowEquiv X }

/-- A subset of a topological space is homeomorphic to its image under a homeomorphism.
-/
@[simps!]
def image (e : X ‚âÉ‚Çú Y) (s : Set X) : s ‚âÉ‚Çú e '' s where
  -- TODO: by continuity!
  continuous_toFun := e.continuous.continuousOn.restrict_mapsTo (mapsTo_image _ _)
  continuous_invFun := (e.symm.continuous.comp continuous_subtype_val).codRestrict _
  toEquiv := e.toEquiv.image s

/-- `Set.univ X` is homeomorphic to `X`. -/
@[simps! -fullyApplied]
def Set.univ (X : Type*) [TopologicalSpace X] : (univ : Set X) ‚âÉ‚Çú X where
  toEquiv := Equiv.Set.univ X
  continuous_toFun := continuous_subtype_val
  continuous_invFun := continuous_id.subtype_mk _

/-- `s √óÀ¢ t` is homeomorphic to `s √ó t`. -/
@[simps!]
def Set.prod (s : Set X) (t : Set Y) : ‚Ü•(s √óÀ¢ t) ‚âÉ‚Çú s √ó t where
  toEquiv := Equiv.Set.prod s t
  continuous_toFun :=
    (continuous_subtype_val.fst.subtype_mk _).prodMk (continuous_subtype_val.snd.subtype_mk _)
  continuous_invFun :=
    (continuous_subtype_val.fst'.prodMk continuous_subtype_val.snd').subtype_mk _

section

variable {Œπ : Type*}

/-- The topological space `Œ† i, Y i` can be split as a product by separating the indices in Œπ
  depending on whether they satisfy a predicate p or not. -/
@[simps!]
def piEquivPiSubtypeProd (p : Œπ ‚Üí Prop) (Y : Œπ ‚Üí Type*) [‚àÄ i, TopologicalSpace (Y i)]
    [DecidablePred p] : (‚àÄ i, Y i) ‚âÉ‚Çú (‚àÄ i : { x // p x }, Y i) √ó ‚àÄ i : { x // ¬¨p x }, Y i where
  toEquiv := Equiv.piEquivPiSubtypeProd p Y
  continuous_toFun := by
    apply Continuous.prodMk <;> exact continuous_pi fun j => continuous_apply j.1
  continuous_invFun :=
    continuous_pi fun j => by
      dsimp only [Equiv.piEquivPiSubtypeProd]; split_ifs
      exacts [(continuous_apply _).comp continuous_fst, (continuous_apply _).comp continuous_snd]

variable [DecidableEq Œπ] (i : Œπ)

/-- A product of topological spaces can be split as the binary product of one of the spaces and
  the product of all the remaining spaces. -/
@[simps!]
def piSplitAt (Y : Œπ ‚Üí Type*) [‚àÄ j, TopologicalSpace (Y j)] :
    (‚àÄ j, Y j) ‚âÉ‚Çú Y i √ó ‚àÄ j : { j // j ‚â† i }, Y j where
  toEquiv := Equiv.piSplitAt i Y
  continuous_toFun := (continuous_apply i).prodMk (continuous_pi fun j => continuous_apply j.1)
  continuous_invFun :=
    continuous_pi fun j => by
      dsimp only [Equiv.piSplitAt]
      split_ifs with h
      ¬∑ subst h
        exact continuous_fst
      ¬∑ exact (continuous_apply _).comp continuous_snd

variable (Y)

/-- A product of copies of a topological space can be split as the binary product of one copy and
  the product of all the remaining copies. -/
@[simps!]
def funSplitAt : (Œπ ‚Üí Y) ‚âÉ‚Çú Y √ó ({ j // j ‚â† i } ‚Üí Y) :=
  piSplitAt i _

end

end Homeomorph

namespace Topology.IsEmbedding

/-- Homeomorphism given an embedding. -/
@[simps! apply_coe]
noncomputable def toHomeomorph {f : X ‚Üí Y} (hf : IsEmbedding f) :
    X ‚âÉ‚Çú Set.range f :=
  Equiv.ofInjective f hf.injective |>.toHomeomorphOfIsInducing <|
    IsInducing.subtypeVal.of_comp_iff.mp hf.toIsInducing

@[deprecated (since := "2025-04-16")]
alias _root_.Homeomorph.ofIsEmbedding := toHomeomorph

/-- A surjective embedding is a homeomorphism. -/
@[simps! apply]
noncomputable def toHomeomorphOfSurjective {f : X ‚Üí Y}
    (hf : IsEmbedding f) (hsurj : Function.Surjective f) : X ‚âÉ‚Çú Y :=
  Equiv.ofBijective f ‚ü®hf.injective, hsurj‚ü© |>.toHomeomorphOfIsInducing hf.toIsInducing

@[deprecated (since := "2025-04-16")]
alias toHomeomorph_of_surjective := toHomeomorphOfSurjective

/-- A set is homeomorphic to its image under any embedding. -/
noncomputable def homeomorphImage {f : X ‚Üí Y} (hf : IsEmbedding f) (s : Set X) : s ‚âÉ‚Çú f '' s :=
  (hf.comp .subtypeVal).toHomeomorph.trans <| .setCongr <| by simp [Set.range_comp]

end Topology.IsEmbedding

end

namespace Continuous

variable [TopologicalSpace X] [TopologicalSpace Y]

theorem continuous_symm_of_equiv_compact_to_t2 [CompactSpace X] [T2Space Y] {f : X ‚âÉ Y}
    (hf : Continuous f) : Continuous f.symm := by
  rw [continuous_iff_isClosed]
  intro C hC
  have hC' : IsClosed (f '' C) := (hC.isCompact.image hf).isClosed
  rwa [Equiv.image_eq_preimage] at hC'

/-- Continuous equivalences from a compact space to a T2 space are homeomorphisms.

This is not true when T2 is weakened to T1
(see `Continuous.homeoOfEquivCompactToT2.t1_counterexample`). -/
@[simps toEquiv]
def homeoOfEquivCompactToT2 [CompactSpace X] [T2Space Y] {f : X ‚âÉ Y} (hf : Continuous f) : X ‚âÉ‚Çú Y :=
  { f with
    continuous_toFun := hf
    continuous_invFun := hf.continuous_symm_of_equiv_compact_to_t2 }

end Continuous

variable [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]
  {W : Type*} [TopologicalSpace W] {f : X ‚Üí Y}

namespace IsHomeomorph
variable (hf : IsHomeomorph f)
include hf

variable (f) in
/-- Bundled homeomorphism constructed from a map that is a homeomorphism. -/
@[simps! toEquiv apply symm_apply]
noncomputable def homeomorph : X ‚âÉ‚Çú Y where
  continuous_toFun := hf.1
  continuous_invFun := by
    rw [‚Üê continuousOn_univ, ‚Üê hf.bijective.2.range_eq]
    exact hf.isOpenMap.continuousOn_range_of_leftInverse (leftInverse_surjInv hf.bijective)
  toEquiv := Equiv.ofBijective f hf.bijective

protected lemma isClosedMap : IsClosedMap f := (hf.homeomorph f).isClosedMap
lemma isInducing : IsInducing f := (hf.homeomorph f).isInducing
lemma isQuotientMap : IsQuotientMap f := (hf.homeomorph f).isQuotientMap
lemma isEmbedding : IsEmbedding f := (hf.homeomorph f).isEmbedding
lemma isOpenEmbedding : IsOpenEmbedding f := (hf.homeomorph f).isOpenEmbedding
lemma isClosedEmbedding : IsClosedEmbedding f := (hf.homeomorph f).isClosedEmbedding
lemma isDenseEmbedding : IsDenseEmbedding f := (hf.homeomorph f).isDenseEmbedding

end IsHomeomorph

/-- A map is a homeomorphism iff it is the map underlying a bundled homeomorphism `h : X ‚âÉ‚Çú Y`. -/
lemma isHomeomorph_iff_exists_homeomorph : IsHomeomorph f ‚Üî ‚àÉ h : X ‚âÉ‚Çú Y, h = f :=
  ‚ü®fun hf => ‚ü®hf.homeomorph f, rfl‚ü©, fun ‚ü®h, h'‚ü© => h' ‚ñ∏ h.isHomeomorph‚ü©

/-- A map is a homeomorphism iff it is continuous and has a continuous inverse. -/
lemma isHomeomorph_iff_exists_inverse : IsHomeomorph f ‚Üî Continuous f ‚àß ‚àÉ g : Y ‚Üí X,
    LeftInverse g f ‚àß RightInverse g f ‚àß Continuous g := by
  refine ‚ü®fun hf ‚Ü¶ ‚ü®hf.continuous, ?_‚ü©, fun ‚ü®hf, g, hg‚ü© ‚Ü¶ ?_‚ü©
  ¬∑ let h := hf.homeomorph f
    exact ‚ü®h.symm, h.left_inv, h.right_inv, h.continuous_invFun‚ü©
  ¬∑ exact (Homeomorph.mk ‚ü®f, g, hg.1, hg.2.1‚ü© hf hg.2.2).isHomeomorph

/-- A map is a homeomorphism iff it is a surjective embedding. -/
lemma isHomeomorph_iff_isEmbedding_surjective : IsHomeomorph f ‚Üî IsEmbedding f ‚àß Surjective f where
  mp hf := ‚ü®hf.isEmbedding, hf.surjective‚ü©
  mpr h := ‚ü®h.1.continuous, ((isOpenEmbedding_iff f).2 ‚ü®h.1, h.2.range_eq ‚ñ∏ isOpen_univ‚ü©).isOpenMap,
    h.1.injective, h.2‚ü©

/-- A map is a homeomorphism iff it is continuous, closed and bijective. -/
lemma isHomeomorph_iff_continuous_isClosedMap_bijective : IsHomeomorph f ‚Üî
    Continuous f ‚àß IsClosedMap f ‚àß Function.Bijective f :=
  ‚ü®fun hf => ‚ü®hf.continuous, hf.isClosedMap, hf.bijective‚ü©, fun ‚ü®hf, hf', hf''‚ü© =>
    ‚ü®hf, fun _ hu => isClosed_compl_iff.1 (image_compl_eq hf'' ‚ñ∏ hf' _ hu.isClosed_compl), hf''‚ü©‚ü©

/-- A map from a compact space to a T2 space is a homeomorphism iff it is continuous and
  bijective. -/
lemma isHomeomorph_iff_continuous_bijective [CompactSpace X] [T2Space Y] :
    IsHomeomorph f ‚Üî Continuous f ‚àß Bijective f := by
  rw [isHomeomorph_iff_continuous_isClosedMap_bijective]
  refine and_congr_right fun hf ‚Ü¶ ?_
  rw [eq_true hf.isClosedMap, true_and]

lemma IsHomeomorph.sumMap {g : Z ‚Üí W} (hf : IsHomeomorph f) (hg : IsHomeomorph g) :
    IsHomeomorph (Sum.map f g) := ‚ü®hf.1.sumMap hg.1, hf.2.sumMap hg.2, hf.3.sumMap hg.3‚ü©

lemma IsHomeomorph.prodMap {g : Z ‚Üí W} (hf : IsHomeomorph f) (hg : IsHomeomorph g) :
    IsHomeomorph (Prod.map f g) := ‚ü®hf.1.prodMap hg.1, hf.2.prodMap hg.2, hf.3.prodMap hg.3‚ü©

lemma IsHomeomorph.sigmaMap {Œπ Œ∫ : Type*} {X : Œπ ‚Üí Type*} {Y : Œ∫ ‚Üí Type*}
    [‚àÄ i, TopologicalSpace (X i)] [‚àÄ i, TopologicalSpace (Y i)] {f : Œπ ‚Üí Œ∫}
    (hf : Bijective f) {g : (i : Œπ) ‚Üí X i ‚Üí Y (f i)} (hg : ‚àÄ i, IsHomeomorph (g i)) :
    IsHomeomorph (Sigma.map f g) := by
  simp_rw [isHomeomorph_iff_isEmbedding_surjective,] at hg ‚ä¢
  exact ‚ü®(isEmbedding_sigmaMap hf.1).2 fun i ‚Ü¶ (hg i).1, hf.2.sigma_map fun i ‚Ü¶ (hg i).2‚ü©

lemma IsHomeomorph.pi_map {Œπ : Type*} {X Y : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (X i)]
    [‚àÄ i, TopologicalSpace (Y i)] {f : (i : Œπ) ‚Üí X i ‚Üí Y i} (h : ‚àÄ i, IsHomeomorph (f i)) :
    IsHomeomorph (fun (x : ‚àÄ i, X i) i ‚Ü¶ f i (x i)) :=
  (Homeomorph.piCongrRight fun i ‚Ü¶ (h i).homeomorph (f i)).isHomeomorph
