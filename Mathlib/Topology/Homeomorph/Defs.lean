/-
Copyright (c) 2019 Reid Barton. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Patrick Massot, SÃ©bastien GouÃ«zel, Zhouhang Zhou, Reid Barton
-/
import Mathlib.Topology.ContinuousMap.Defs
import Mathlib.Topology.Maps.Basic

/-!
# Homeomorphisms

This file defines homeomorphisms between two topological spaces. They are bijections with both
directions continuous. We denote homeomorphisms with the notation `â‰ƒâ‚œ`.

# Main definitions and results

* `Homeomorph X Y`: The type of homeomorphisms from `X` to `Y`.
  This type can be denoted using the following notation: `X â‰ƒâ‚œ Y`.
* `HomeomorphClass`: `HomeomorphClass F A B` states that `F` is a type of homeomorphisms.

* `Homeomorph.symm`: the inverse of a homeomorphism
* `Homeomorph.trans`: composing two homeomorphisms
* Homeomorphisms are open and closed embeddings, inducing, quotient maps etc.
* `Homeomorph.homeomorphOfContinuousOpen`: A continuous bijection that is
  an open map is a homeomorphism.
* `Homeomorph.homeomorphOfUnique`: if both `X` and `Y` have a unique element, then `X â‰ƒâ‚œ Y`.
* `Equiv.toHomeomorph`: an equivalence between topological spaces respecting openness
  is a homeomorphism.

* `IsHomeomorph`: the predicate that a function is a homeomorphism

-/

open Set Topology Filter

variable {X Y W Z : Type*}

/-- Homeomorphism between `X` and `Y`, also called topological isomorphism -/
structure Homeomorph (X : Type*) (Y : Type*) [TopologicalSpace X] [TopologicalSpace Y]
    extends X â‰ƒ Y where
  /-- The forward map of a homeomorphism is a continuous function. -/
  continuous_toFun : Continuous toFun := by continuity
  /-- The inverse map of a homeomorphism is a continuous function. -/
  continuous_invFun : Continuous invFun := by continuity

@[inherit_doc]
infixl:25 " â‰ƒâ‚œ " => Homeomorph

namespace Homeomorph

variable [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace W] [TopologicalSpace Z]
  {X' Y' : Type*} [TopologicalSpace X'] [TopologicalSpace Y']

theorem toEquiv_injective : Function.Injective (toEquiv : X â‰ƒâ‚œ Y â†’ X â‰ƒ Y)
  | âŸ¨_, _, _âŸ©, âŸ¨_, _, _âŸ©, rfl => rfl

instance : EquivLike (X â‰ƒâ‚œ Y) X Y where
  coe h := h.toEquiv
  inv h := h.toEquiv.symm
  left_inv h := h.left_inv
  right_inv h := h.right_inv
  coe_injective' _ _ H _ := toEquiv_injective <| DFunLike.ext' H

@[simp] theorem homeomorph_mk_coe (a : X â‰ƒ Y) (b c) : (Homeomorph.mk a b c : X â†’ Y) = a :=
  rfl

/-- The unique homeomorphism between two empty types. -/
protected def empty [IsEmpty X] [IsEmpty Y] : X â‰ƒâ‚œ Y where
  __ := Equiv.equivOfIsEmpty X Y

/-- Inverse of a homeomorphism. -/
@[symm]
protected def symm (h : X â‰ƒâ‚œ Y) : Y â‰ƒâ‚œ X where
  continuous_toFun := h.continuous_invFun
  continuous_invFun := h.continuous_toFun
  toEquiv := h.toEquiv.symm

@[simp] theorem symm_symm (h : X â‰ƒâ‚œ Y) : h.symm.symm = h := rfl

theorem symm_bijective : Function.Bijective (Homeomorph.symm : (X â‰ƒâ‚œ Y) â†’ Y â‰ƒâ‚œ X) :=
  Function.bijective_iff_has_inverse.mpr âŸ¨_, symm_symm, symm_symmâŸ©

/-- See Note [custom simps projection] -/
def Simps.symm_apply (h : X â‰ƒâ‚œ Y) : Y â†’ X :=
  h.symm

initialize_simps_projections Homeomorph (toFun â†’ apply, invFun â†’ symm_apply)

@[simp]
theorem coe_toEquiv (h : X â‰ƒâ‚œ Y) : â‡‘h.toEquiv = h :=
  rfl

@[simp]
theorem coe_symm_toEquiv (h : X â‰ƒâ‚œ Y) : â‡‘h.toEquiv.symm = h.symm :=
  rfl

@[ext]
theorem ext {h h' : X â‰ƒâ‚œ Y} (H : âˆ€ x, h x = h' x) : h = h' :=
  DFunLike.ext _ _ H

/-- Identity map as a homeomorphism. -/
@[simps! -fullyApplied apply]
protected def refl (X : Type*) [TopologicalSpace X] : X â‰ƒâ‚œ X where
  continuous_toFun := continuous_id
  continuous_invFun := continuous_id
  toEquiv := Equiv.refl X

/-- Composition of two homeomorphisms. -/
@[trans]
protected def trans (hâ‚ : X â‰ƒâ‚œ Y) (hâ‚‚ : Y â‰ƒâ‚œ Z) : X â‰ƒâ‚œ Z where
  continuous_toFun := hâ‚‚.continuous_toFun.comp hâ‚.continuous_toFun
  continuous_invFun := hâ‚.continuous_invFun.comp hâ‚‚.continuous_invFun
  toEquiv := Equiv.trans hâ‚.toEquiv hâ‚‚.toEquiv

@[simp]
theorem trans_apply (hâ‚ : X â‰ƒâ‚œ Y) (hâ‚‚ : Y â‰ƒâ‚œ Z) (x : X) : hâ‚.trans hâ‚‚ x = hâ‚‚ (hâ‚ x) :=
  rfl

@[simp]
theorem symm_trans_apply (f : X â‰ƒâ‚œ Y) (g : Y â‰ƒâ‚œ Z) (z : Z) :
    (f.trans g).symm z = f.symm (g.symm z) := rfl

@[simp]
theorem homeomorph_mk_coe_symm (a : X â‰ƒ Y) (b c) :
    ((Homeomorph.mk a b c).symm : Y â†’ X) = a.symm :=
  rfl

@[simp]
theorem refl_symm : (Homeomorph.refl X).symm = Homeomorph.refl X :=
  rfl

@[continuity, fun_prop]
protected theorem continuous (h : X â‰ƒâ‚œ Y) : Continuous h :=
  h.continuous_toFun

-- otherwise `by continuity` can't prove continuity of `h.to_equiv.symm`
@[continuity]
protected theorem continuous_symm (h : X â‰ƒâ‚œ Y) : Continuous h.symm :=
  h.continuous_invFun

@[simp]
theorem apply_symm_apply (h : X â‰ƒâ‚œ Y) (y : Y) : h (h.symm y) = y :=
  h.toEquiv.apply_symm_apply y

@[simp]
theorem symm_apply_apply (h : X â‰ƒâ‚œ Y) (x : X) : h.symm (h x) = x :=
  h.toEquiv.symm_apply_apply x

@[simp]
theorem self_trans_symm (h : X â‰ƒâ‚œ Y) : h.trans h.symm = Homeomorph.refl X := by
  ext
  apply symm_apply_apply

@[simp]
theorem symm_trans_self (h : X â‰ƒâ‚œ Y) : h.symm.trans h = Homeomorph.refl Y := by
  ext
  apply apply_symm_apply

protected theorem bijective (h : X â‰ƒâ‚œ Y) : Function.Bijective h :=
  h.toEquiv.bijective

protected theorem injective (h : X â‰ƒâ‚œ Y) : Function.Injective h :=
  h.toEquiv.injective

protected theorem surjective (h : X â‰ƒâ‚œ Y) : Function.Surjective h :=
  h.toEquiv.surjective

/-- Change the homeomorphism `f` to make the inverse function definitionally equal to `g`. -/
def changeInv (f : X â‰ƒâ‚œ Y) (g : Y â†’ X) (hg : Function.RightInverse g f) : X â‰ƒâ‚œ Y :=
  haveI : g = f.symm := (f.left_inv.eq_rightInverse hg).symm
  { toFun := f
    invFun := g
    left_inv := by convert f.left_inv
    right_inv := by convert f.right_inv using 1
    continuous_toFun := f.continuous
    continuous_invFun := by convert f.symm.continuous }

@[simp]
theorem symm_comp_self (h : X â‰ƒâ‚œ Y) : h.symm âˆ˜ h = id :=
  funext h.symm_apply_apply

@[simp]
theorem self_comp_symm (h : X â‰ƒâ‚œ Y) : h âˆ˜ h.symm = id :=
  funext h.apply_symm_apply

theorem range_coe (h : X â‰ƒâ‚œ Y) : range h = univ := by simp

theorem image_symm (h : X â‰ƒâ‚œ Y) : image h.symm = preimage h :=
  funext h.symm.toEquiv.image_eq_preimage

theorem preimage_symm (h : X â‰ƒâ‚œ Y) : preimage h.symm = image h :=
  (funext h.toEquiv.image_eq_preimage).symm

@[simp]
theorem image_preimage (h : X â‰ƒâ‚œ Y) (s : Set Y) : h '' (h â»Â¹' s) = s :=
  h.toEquiv.image_preimage s

@[simp]
theorem preimage_image (h : X â‰ƒâ‚œ Y) (s : Set X) : h â»Â¹' (h '' s) = s :=
  h.toEquiv.preimage_image s

theorem image_eq_preimage (h : X â‰ƒâ‚œ Y) (s : Set X) : h '' s = h.symm â»Â¹' s :=
  h.toEquiv.image_eq_preimage s

lemma image_compl (h : X â‰ƒâ‚œ Y) (s : Set X) : h '' (sá¶œ) = (h '' s)á¶œ :=
  h.toEquiv.image_compl s

lemma isInducing (h : X â‰ƒâ‚œ Y) : IsInducing h :=
  .of_comp h.continuous h.symm.continuous <| by simp only [symm_comp_self, IsInducing.id]

@[deprecated (since := "2024-10-28")] alias inducing := isInducing

theorem induced_eq (h : X â‰ƒâ‚œ Y) : TopologicalSpace.induced h â€¹_â€º = â€¹_â€º := h.isInducing.1.symm

theorem isQuotientMap (h : X â‰ƒâ‚œ Y) : IsQuotientMap h :=
  IsQuotientMap.of_comp h.symm.continuous h.continuous <| by
    simp only [self_comp_symm, IsQuotientMap.id]

@[deprecated (since := "2024-10-22")]
alias quotientMap := isQuotientMap

theorem coinduced_eq (h : X â‰ƒâ‚œ Y) : TopologicalSpace.coinduced h â€¹_â€º = â€¹_â€º :=
  h.isQuotientMap.2.symm

theorem isEmbedding (h : X â‰ƒâ‚œ Y) : IsEmbedding h := âŸ¨h.isInducing, h.injectiveâŸ©

@[deprecated (since := "2024-10-26")]
alias embedding := isEmbedding

@[simp]
theorem isOpen_preimage (h : X â‰ƒâ‚œ Y) {s : Set Y} : IsOpen (h â»Â¹' s) â†” IsOpen s :=
  h.isQuotientMap.isOpen_preimage

@[simp]
theorem isOpen_image (h : X â‰ƒâ‚œ Y) {s : Set X} : IsOpen (h '' s) â†” IsOpen s := by
  rw [â† preimage_symm, isOpen_preimage]

protected theorem isOpenMap (h : X â‰ƒâ‚œ Y) : IsOpenMap h := fun _ => h.isOpen_image.2

protected theorem isOpenQuotientMap (h : X â‰ƒâ‚œ Y) : IsOpenQuotientMap h :=
  âŸ¨h.surjective, h.continuous, h.isOpenMapâŸ©

@[simp]
theorem isClosed_preimage (h : X â‰ƒâ‚œ Y) {s : Set Y} : IsClosed (h â»Â¹' s) â†” IsClosed s := by
  simp only [â† isOpen_compl_iff, â† preimage_compl, isOpen_preimage]

@[simp]
theorem isClosed_image (h : X â‰ƒâ‚œ Y) {s : Set X} : IsClosed (h '' s) â†” IsClosed s := by
  rw [â† preimage_symm, isClosed_preimage]

protected theorem isClosedMap (h : X â‰ƒâ‚œ Y) : IsClosedMap h := fun _ => h.isClosed_image.2

theorem isOpenEmbedding (h : X â‰ƒâ‚œ Y) : IsOpenEmbedding h :=
  .of_isEmbedding_isOpenMap h.isEmbedding h.isOpenMap

theorem isClosedEmbedding (h : X â‰ƒâ‚œ Y) : IsClosedEmbedding h :=
  .of_isEmbedding_isClosedMap h.isEmbedding h.isClosedMap

@[deprecated (since := "2024-10-20")]
alias closedEmbedding := isClosedEmbedding

theorem preimage_closure (h : X â‰ƒâ‚œ Y) (s : Set Y) : h â»Â¹' closure s = closure (h â»Â¹' s) :=
  h.isOpenMap.preimage_closure_eq_closure_preimage h.continuous _

theorem image_closure (h : X â‰ƒâ‚œ Y) (s : Set X) : h '' closure s = closure (h '' s) := by
  rw [â† preimage_symm, preimage_closure]

theorem preimage_interior (h : X â‰ƒâ‚œ Y) (s : Set Y) : h â»Â¹' interior s = interior (h â»Â¹' s) :=
  h.isOpenMap.preimage_interior_eq_interior_preimage h.continuous _

theorem image_interior (h : X â‰ƒâ‚œ Y) (s : Set X) : h '' interior s = interior (h '' s) := by
  rw [â† preimage_symm, preimage_interior]

theorem preimage_frontier (h : X â‰ƒâ‚œ Y) (s : Set Y) : h â»Â¹' frontier s = frontier (h â»Â¹' s) :=
  h.isOpenMap.preimage_frontier_eq_frontier_preimage h.continuous _

theorem image_frontier (h : X â‰ƒâ‚œ Y) (s : Set X) : h '' frontier s = frontier (h '' s) := by
  rw [â† preimage_symm, preimage_frontier]

/-- If a bijective map `e : X â‰ƒ Y` is continuous and open, then it is a homeomorphism. -/
@[simps toEquiv]
def homeomorphOfContinuousOpen (e : X â‰ƒ Y) (hâ‚ : Continuous e) (hâ‚‚ : IsOpenMap e) : X â‰ƒâ‚œ Y where
  continuous_toFun := hâ‚
  continuous_invFun := e.continuous_symm_iff.2 hâ‚‚
  toEquiv := e

/-- If a bijective map `e : X â‰ƒ Y` is continuous and closed, then it is a homeomorphism. -/
def homeomorphOfContinuousClosed (e : X â‰ƒ Y) (hâ‚ : Continuous e) (hâ‚‚ : IsClosedMap e) : X â‰ƒâ‚œ Y where
  continuous_toFun := hâ‚
  continuous_invFun := by
    rw [continuous_iff_isClosed]
    intro s hs
    convert â† hâ‚‚ s hs using 1
    apply e.image_eq_preimage
  toEquiv := e

@[simp]
theorem homeomorphOfContinuousOpen_apply (e : X â‰ƒ Y) (hâ‚ : Continuous e) (hâ‚‚ : IsOpenMap e) :
    â‡‘(homeomorphOfContinuousOpen e hâ‚ hâ‚‚) = e := rfl

@[simp]
theorem homeomorphOfContinuousOpen_symm_apply (e : X â‰ƒ Y) (hâ‚ : Continuous e) (hâ‚‚ : IsOpenMap e) :
    â‡‘(homeomorphOfContinuousOpen e hâ‚ hâ‚‚).symm = e.symm := rfl

@[simp]
theorem comp_continuous_iff (h : X â‰ƒâ‚œ Y) {f : Z â†’ X} : Continuous (h âˆ˜ f) â†” Continuous f :=
  h.isInducing.continuous_iff.symm

@[simp]
theorem comp_continuous_iff' (h : X â‰ƒâ‚œ Y) {f : Y â†’ Z} : Continuous (f âˆ˜ h) â†” Continuous f :=
  h.isQuotientMap.continuous_iff.symm

theorem comp_continuousAt_iff (h : X â‰ƒâ‚œ Y) (f : Z â†’ X) (z : Z) :
    ContinuousAt (h âˆ˜ f) z â†” ContinuousAt f z :=
  h.isInducing.continuousAt_iff.symm

theorem comp_continuousAt_iff' (h : X â‰ƒâ‚œ Y) (f : Y â†’ Z) (x : X) :
    ContinuousAt (f âˆ˜ h) x â†” ContinuousAt f (h x) :=
  h.isInducing.continuousAt_iff' (by simp)

@[simp]
theorem comp_isOpenMap_iff (h : X â‰ƒâ‚œ Y) {f : Z â†’ X} : IsOpenMap (h âˆ˜ f) â†” IsOpenMap f := by
  refine âŸ¨?_, fun hf => h.isOpenMap.comp hfâŸ©
  intro hf
  rw [â† Function.id_comp f, â† h.symm_comp_self, Function.comp_assoc]
  exact h.symm.isOpenMap.comp hf

@[simp]
theorem comp_isOpenMap_iff' (h : X â‰ƒâ‚œ Y) {f : Y â†’ Z} : IsOpenMap (f âˆ˜ h) â†” IsOpenMap f := by
  refine âŸ¨?_, fun hf => hf.comp h.isOpenMapâŸ©
  intro hf
  rw [â† Function.comp_id f, â† h.self_comp_symm, â† Function.comp_assoc]
  exact hf.comp h.symm.isOpenMap

variable (X Y) in
/-- If both `X` and `Y` have a unique element, then `X â‰ƒâ‚œ Y`. -/
@[simps!]
def homeomorphOfUnique [Unique X] [Unique Y] : X â‰ƒâ‚œ Y :=
  { Equiv.ofUnique X Y with
    continuous_toFun := continuous_const
    continuous_invFun := continuous_const }

@[simp]
theorem map_nhds_eq (h : X â‰ƒâ‚œ Y) (x : X) : map h (ğ“ x) = ğ“ (h x) :=
  h.isEmbedding.map_nhds_of_mem _ (by simp)

theorem symm_map_nhds_eq (h : X â‰ƒâ‚œ Y) (x : X) : map h.symm (ğ“ (h x)) = ğ“ x := by
  rw [h.symm.map_nhds_eq, h.symm_apply_apply]

theorem nhds_eq_comap (h : X â‰ƒâ‚œ Y) (x : X) : ğ“ x = comap h (ğ“ (h x)) :=
  h.isInducing.nhds_eq_comap x

@[simp]
theorem comap_nhds_eq (h : X â‰ƒâ‚œ Y) (y : Y) : comap h (ğ“ y) = ğ“ (h.symm y) := by
  rw [h.nhds_eq_comap, h.apply_symm_apply]

end Homeomorph

namespace Equiv
variable {Z : Type*} [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]

/-- An equivalence between topological spaces respecting openness is a homeomorphism. -/
@[simps toEquiv]
def toHomeomorph (e : X â‰ƒ Y) (he : âˆ€ s, IsOpen (e â»Â¹' s) â†” IsOpen s) : X â‰ƒâ‚œ Y where
  toEquiv := e
  continuous_toFun := continuous_def.2 fun _ â†¦ (he _).2
  continuous_invFun := continuous_def.2 fun s â†¦ by convert (he _).1; simp

@[simp] lemma coe_toHomeomorph (e : X â‰ƒ Y) (he) : â‡‘(e.toHomeomorph he) = e := rfl
lemma toHomeomorph_apply (e : X â‰ƒ Y) (he) (x : X) : e.toHomeomorph he x = e x := rfl

@[simp] lemma toHomeomorph_refl :
  (Equiv.refl X).toHomeomorph (fun _s â†¦ Iff.rfl) = Homeomorph.refl _ := rfl

@[simp] lemma toHomeomorph_symm (e : X â‰ƒ Y) (he) :
  (e.toHomeomorph he).symm = e.symm.toHomeomorph fun s â†¦ by convert (he _).symm; simp := rfl

lemma toHomeomorph_trans (e : X â‰ƒ Y) (f : Y â‰ƒ Z) (he hf) :
    (e.trans f).toHomeomorph (fun _s â†¦ (he _).trans (hf _)) =
    (e.toHomeomorph he).trans (f.toHomeomorph hf) := rfl

/-- An inducing equiv between topological spaces is a homeomorphism. -/
@[simps toEquiv]
def toHomeomorphOfIsInducing (f : X â‰ƒ Y) (hf : IsInducing f) : X â‰ƒâ‚œ Y :=
  { f with
    continuous_toFun := hf.continuous
    continuous_invFun := hf.continuous_iff.2 <| by simpa using continuous_id }

@[deprecated (since := "2024-10-28")] alias toHomeomorphOfInducing := toHomeomorphOfIsInducing

end Equiv

/-- `HomeomorphClass F A B` states that `F` is a type of homeomorphisms. -/
class HomeomorphClass (F : Type*) (A B : outParam Type*)
    [TopologicalSpace A] [TopologicalSpace B] [h : EquivLike F A B] : Prop where
  map_continuous : âˆ€ (f : F), Continuous f
  inv_continuous : âˆ€ (f : F), Continuous (h.inv f)

namespace HomeomorphClass

variable {F Î± Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²] [EquivLike F Î± Î²]

/-- Turn an element of a type `F` satisfying `HomeomorphClass F Î± Î²` into an actual
`Homeomorph`. This is declared as the default coercion from `F` to `Î± â‰ƒâ‚œ Î²`. -/
@[coe]
def toHomeomorph [h : HomeomorphClass F Î± Î²] (f : F) : Î± â‰ƒâ‚œ Î² :=
  { (f : Î± â‰ƒ Î²) with
    continuous_toFun := h.map_continuous f
    continuous_invFun := h.inv_continuous f }

@[simp]
theorem coe_coe [h : HomeomorphClass F Î± Î²] (f : F) : â‡‘(h.toHomeomorph f) = â‡‘f := rfl

instance [HomeomorphClass F Î± Î²] : CoeOut F (Î± â‰ƒâ‚œ Î²) :=
  âŸ¨HomeomorphClass.toHomeomorphâŸ©

theorem toHomeomorph_injective [HomeomorphClass F Î± Î²] : Function.Injective ((â†‘) : F â†’ Î± â‰ƒâ‚œ Î²) :=
  fun _ _ e â†¦ DFunLike.ext _ _ fun a â†¦ congr_arg (fun e : Î± â‰ƒâ‚œ Î² â†¦ e.toFun a) e

instance [HomeomorphClass F Î± Î²] : ContinuousMapClass F Î± Î² where
  map_continuous  f := map_continuous f

instance : HomeomorphClass (Î± â‰ƒâ‚œ Î²) Î± Î² where
  map_continuous e := e.continuous_toFun
  inv_continuous e := e.continuous_invFun

end HomeomorphClass

section IsHomeomorph

variable [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z] {f : X â†’ Y}

/-- Predicate saying that `f` is a homeomorphism.

This should be used only when `f` is a concrete function whose continuous inverse is not easy to
write down. Otherwise, `Homeomorph` should be preferred as it bundles the continuous inverse.

Having both `Homeomorph` and `IsHomeomorph` is justified by the fact that so many function
properties are unbundled in the topology part of the library, and by the fact that a homeomorphism
is not merely a continuous bijection, that is `IsHomeomorph f` is not equivalent to
`Continuous f âˆ§ Bijective f` but to `Continuous f âˆ§ Bijective f âˆ§ IsOpenMap f`. -/
structure IsHomeomorph (f : X â†’ Y) : Prop where
  continuous : Continuous f
  isOpenMap : IsOpenMap f
  bijective : Function.Bijective f

protected theorem Homeomorph.isHomeomorph (h : X â‰ƒâ‚œ Y) : IsHomeomorph h :=
  âŸ¨h.continuous, h.isOpenMap, h.bijectiveâŸ©

namespace IsHomeomorph

protected lemma injective (hf : IsHomeomorph f) : Function.Injective f := hf.bijective.injective
protected lemma surjective (hf : IsHomeomorph f) : Function.Surjective f := hf.bijective.surjective

protected lemma id : IsHomeomorph (@id X) := âŸ¨continuous_id, .id, Function.bijective_idâŸ©

lemma comp {g : Y â†’ Z} (hg : IsHomeomorph g) (hf : IsHomeomorph f) : IsHomeomorph (g âˆ˜ f) :=
  âŸ¨hg.1.comp hf.1, hg.2.comp hf.2, hg.3.comp hf.3âŸ©

end IsHomeomorph

end IsHomeomorph
