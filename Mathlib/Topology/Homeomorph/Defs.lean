/-
Copyright (c) 2019 Reid Barton. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Patrick Massot, S√©bastien Gou√´zel, Zhouhang Zhou, Reid Barton
-/
import Mathlib.Topology.ContinuousMap.Defs
import Mathlib.Topology.Maps.Basic

/-!
# Homeomorphisms

This file defines homeomorphisms between two topological spaces. They are bijections with both
directions continuous. We denote homeomorphisms with the notation `‚âÉ‚Çú`.

# Main definitions and results

* `Homeomorph X Y`: The type of homeomorphisms from `X` to `Y`.
  This type can be denoted using the following notation: `X ‚âÉ‚Çú Y`.
* `HomeomorphClass`: `HomeomorphClass F A B` states that `F` is a type of homeomorphisms.

* `Homeomorph.symm`: the inverse of a homeomorphism
* `Homeomorph.trans`: composing two homeomorphisms
* Homeomorphisms are open and closed embeddings, inducing, quotient maps etc.
* `Homeomorph.homeomorphOfContinuousOpen`: A continuous bijection that is
  an open map is a homeomorphism.
* `Homeomorph.homeomorphOfUnique`: if both `X` and `Y` have a unique element, then `X ‚âÉ‚Çú Y`.
* `Equiv.toHomeomorph`: an equivalence between topological spaces respecting openness
  is a homeomorphism.

* `IsHomeomorph`: the predicate that a function is a homeomorphism

-/

open Set Topology Filter

variable {X Y W Z : Type*}

/-- Homeomorphism between `X` and `Y`, also called topological isomorphism -/
structure Homeomorph (X : Type*) (Y : Type*) [TopologicalSpace X] [TopologicalSpace Y]
    extends X ‚âÉ Y where
  /-- The forward map of a homeomorphism is a continuous function. -/
  continuous_toFun : Continuous toFun := by continuity
  /-- The inverse map of a homeomorphism is a continuous function. -/
  continuous_invFun : Continuous invFun := by continuity

@[inherit_doc]
infixl:25 " ‚âÉ‚Çú " => Homeomorph

namespace Homeomorph

variable [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace W] [TopologicalSpace Z]
  {X' Y' : Type*} [TopologicalSpace X'] [TopologicalSpace Y']

theorem toEquiv_injective : Function.Injective (toEquiv : X ‚âÉ‚Çú Y ‚Üí X ‚âÉ Y)
  | ‚ü®_, _, _‚ü©, ‚ü®_, _, _‚ü©, rfl => rfl

instance : EquivLike (X ‚âÉ‚Çú Y) X Y where
  coe h := h.toEquiv
  inv h := h.toEquiv.symm
  left_inv h := h.left_inv
  right_inv h := h.right_inv
  coe_injective' _ _ H _ := toEquiv_injective <| DFunLike.ext' H

@[simp] theorem homeomorph_mk_coe (a : X ‚âÉ Y) (b c) : (Homeomorph.mk a b c : X ‚Üí Y) = a :=
  rfl

/-- The unique homeomorphism between two empty types. -/
protected def empty [IsEmpty X] [IsEmpty Y] : X ‚âÉ‚Çú Y where
  __ := Equiv.equivOfIsEmpty X Y

/-- Inverse of a homeomorphism. -/
@[symm]
protected def symm (h : X ‚âÉ‚Çú Y) : Y ‚âÉ‚Çú X where
  continuous_toFun := h.continuous_invFun
  continuous_invFun := h.continuous_toFun
  toEquiv := h.toEquiv.symm

@[simp] theorem symm_symm (h : X ‚âÉ‚Çú Y) : h.symm.symm = h := rfl

theorem symm_bijective : Function.Bijective (Homeomorph.symm : (X ‚âÉ‚Çú Y) ‚Üí Y ‚âÉ‚Çú X) :=
  Function.bijective_iff_has_inverse.mpr ‚ü®_, symm_symm, symm_symm‚ü©

/-- See Note [custom simps projection] -/
def Simps.symm_apply (h : X ‚âÉ‚Çú Y) : Y ‚Üí X :=
  h.symm

initialize_simps_projections Homeomorph (toFun ‚Üí apply, invFun ‚Üí symm_apply)

@[simp]
theorem coe_toEquiv (h : X ‚âÉ‚Çú Y) : ‚áëh.toEquiv = h :=
  rfl

@[simp]
theorem coe_symm_toEquiv (h : X ‚âÉ‚Çú Y) : ‚áëh.toEquiv.symm = h.symm :=
  rfl

@[ext]
theorem ext {h h' : X ‚âÉ‚Çú Y} (H : ‚àÄ x, h x = h' x) : h = h' :=
  DFunLike.ext _ _ H

/-- Identity map as a homeomorphism. -/
@[simps! -fullyApplied apply]
protected def refl (X : Type*) [TopologicalSpace X] : X ‚âÉ‚Çú X where
  continuous_toFun := continuous_id
  continuous_invFun := continuous_id
  toEquiv := Equiv.refl X

/-- Composition of two homeomorphisms. -/
@[trans]
protected def trans (h‚ÇÅ : X ‚âÉ‚Çú Y) (h‚ÇÇ : Y ‚âÉ‚Çú Z) : X ‚âÉ‚Çú Z where
  continuous_toFun := h‚ÇÇ.continuous_toFun.comp h‚ÇÅ.continuous_toFun
  continuous_invFun := h‚ÇÅ.continuous_invFun.comp h‚ÇÇ.continuous_invFun
  toEquiv := Equiv.trans h‚ÇÅ.toEquiv h‚ÇÇ.toEquiv

@[simp]
theorem trans_apply (h‚ÇÅ : X ‚âÉ‚Çú Y) (h‚ÇÇ : Y ‚âÉ‚Çú Z) (x : X) : h‚ÇÅ.trans h‚ÇÇ x = h‚ÇÇ (h‚ÇÅ x) :=
  rfl

@[simp]
theorem symm_trans_apply (f : X ‚âÉ‚Çú Y) (g : Y ‚âÉ‚Çú Z) (z : Z) :
    (f.trans g).symm z = f.symm (g.symm z) := rfl

@[simp]
theorem homeomorph_mk_coe_symm (a : X ‚âÉ Y) (b c) :
    ((Homeomorph.mk a b c).symm : Y ‚Üí X) = a.symm :=
  rfl

@[simp]
theorem refl_symm : (Homeomorph.refl X).symm = Homeomorph.refl X :=
  rfl

@[continuity, fun_prop]
protected theorem continuous (h : X ‚âÉ‚Çú Y) : Continuous h :=
  h.continuous_toFun

-- otherwise `by continuity` can't prove continuity of `h.to_equiv.symm`
@[continuity]
protected theorem continuous_symm (h : X ‚âÉ‚Çú Y) : Continuous h.symm :=
  h.continuous_invFun

@[simp]
theorem apply_symm_apply (h : X ‚âÉ‚Çú Y) (y : Y) : h (h.symm y) = y :=
  h.toEquiv.apply_symm_apply y

@[simp]
theorem symm_apply_apply (h : X ‚âÉ‚Çú Y) (x : X) : h.symm (h x) = x :=
  h.toEquiv.symm_apply_apply x

@[simp]
theorem self_trans_symm (h : X ‚âÉ‚Çú Y) : h.trans h.symm = Homeomorph.refl X := by
  ext
  apply symm_apply_apply

@[simp]
theorem symm_trans_self (h : X ‚âÉ‚Çú Y) : h.symm.trans h = Homeomorph.refl Y := by
  ext
  apply apply_symm_apply

protected theorem bijective (h : X ‚âÉ‚Çú Y) : Function.Bijective h :=
  h.toEquiv.bijective

protected theorem injective (h : X ‚âÉ‚Çú Y) : Function.Injective h :=
  h.toEquiv.injective

protected theorem surjective (h : X ‚âÉ‚Çú Y) : Function.Surjective h :=
  h.toEquiv.surjective

/-- Change the homeomorphism `f` to make the inverse function definitionally equal to `g`. -/
def changeInv (f : X ‚âÉ‚Çú Y) (g : Y ‚Üí X) (hg : Function.RightInverse g f) : X ‚âÉ‚Çú Y :=
  haveI : g = f.symm := (f.left_inv.eq_rightInverse hg).symm
  { toFun := f
    invFun := g
    left_inv := by convert f.left_inv
    right_inv := by convert f.right_inv using 1
    continuous_toFun := f.continuous
    continuous_invFun := by convert f.symm.continuous }

@[simp]
theorem symm_comp_self (h : X ‚âÉ‚Çú Y) : h.symm ‚àò h = id :=
  funext h.symm_apply_apply

@[simp]
theorem self_comp_symm (h : X ‚âÉ‚Çú Y) : h ‚àò h.symm = id :=
  funext h.apply_symm_apply

theorem range_coe (h : X ‚âÉ‚Çú Y) : range h = univ := by simp

theorem image_symm (h : X ‚âÉ‚Çú Y) : image h.symm = preimage h :=
  funext h.symm.toEquiv.image_eq_preimage

theorem preimage_symm (h : X ‚âÉ‚Çú Y) : preimage h.symm = image h :=
  (funext h.toEquiv.image_eq_preimage).symm

@[simp]
theorem image_preimage (h : X ‚âÉ‚Çú Y) (s : Set Y) : h '' (h ‚Åª¬π' s) = s :=
  h.toEquiv.image_preimage s

@[simp]
theorem preimage_image (h : X ‚âÉ‚Çú Y) (s : Set X) : h ‚Åª¬π' (h '' s) = s :=
  h.toEquiv.preimage_image s

theorem image_eq_preimage (h : X ‚âÉ‚Çú Y) (s : Set X) : h '' s = h.symm ‚Åª¬π' s :=
  h.toEquiv.image_eq_preimage s

lemma image_compl (h : X ‚âÉ‚Çú Y) (s : Set X) : h '' (s·∂ú) = (h '' s)·∂ú :=
  h.toEquiv.image_compl s

lemma isInducing (h : X ‚âÉ‚Çú Y) : IsInducing h :=
  .of_comp h.continuous h.symm.continuous <| by simp only [symm_comp_self, IsInducing.id]

@[deprecated (since := "2024-10-28")] alias inducing := isInducing

theorem induced_eq (h : X ‚âÉ‚Çú Y) : TopologicalSpace.induced h ‚Äπ_‚Ä∫ = ‚Äπ_‚Ä∫ := h.isInducing.1.symm

theorem isQuotientMap (h : X ‚âÉ‚Çú Y) : IsQuotientMap h :=
  IsQuotientMap.of_comp h.symm.continuous h.continuous <| by
    simp only [self_comp_symm, IsQuotientMap.id]

@[deprecated (since := "2024-10-22")]
alias quotientMap := isQuotientMap

theorem coinduced_eq (h : X ‚âÉ‚Çú Y) : TopologicalSpace.coinduced h ‚Äπ_‚Ä∫ = ‚Äπ_‚Ä∫ :=
  h.isQuotientMap.2.symm

theorem isEmbedding (h : X ‚âÉ‚Çú Y) : IsEmbedding h := ‚ü®h.isInducing, h.injective‚ü©

@[deprecated (since := "2024-10-26")]
alias embedding := isEmbedding

protected theorem discreteTopology [DiscreteTopology X] (h : X ‚âÉ‚Çú Y) : DiscreteTopology Y :=
  h.symm.isEmbedding.discreteTopology

theorem discreteTopology_iff (h : X ‚âÉ‚Çú Y) : DiscreteTopology X ‚Üî DiscreteTopology Y :=
  ‚ü®fun _ ‚Ü¶ h.discreteTopology, fun _ ‚Ü¶ h.symm.discreteTopology‚ü©

@[simp]
theorem isOpen_preimage (h : X ‚âÉ‚Çú Y) {s : Set Y} : IsOpen (h ‚Åª¬π' s) ‚Üî IsOpen s :=
  h.isQuotientMap.isOpen_preimage

@[simp]
theorem isOpen_image (h : X ‚âÉ‚Çú Y) {s : Set X} : IsOpen (h '' s) ‚Üî IsOpen s := by
  rw [‚Üê preimage_symm, isOpen_preimage]

protected theorem isOpenMap (h : X ‚âÉ‚Çú Y) : IsOpenMap h := fun _ => h.isOpen_image.2

protected theorem isOpenQuotientMap (h : X ‚âÉ‚Çú Y) : IsOpenQuotientMap h :=
  ‚ü®h.surjective, h.continuous, h.isOpenMap‚ü©

@[simp]
theorem isClosed_preimage (h : X ‚âÉ‚Çú Y) {s : Set Y} : IsClosed (h ‚Åª¬π' s) ‚Üî IsClosed s := by
  simp only [‚Üê isOpen_compl_iff, ‚Üê preimage_compl, isOpen_preimage]

@[simp]
theorem isClosed_image (h : X ‚âÉ‚Çú Y) {s : Set X} : IsClosed (h '' s) ‚Üî IsClosed s := by
  rw [‚Üê preimage_symm, isClosed_preimage]

protected theorem isClosedMap (h : X ‚âÉ‚Çú Y) : IsClosedMap h := fun _ => h.isClosed_image.2

theorem isOpenEmbedding (h : X ‚âÉ‚Çú Y) : IsOpenEmbedding h :=
  .of_isEmbedding_isOpenMap h.isEmbedding h.isOpenMap

theorem isClosedEmbedding (h : X ‚âÉ‚Çú Y) : IsClosedEmbedding h :=
  .of_isEmbedding_isClosedMap h.isEmbedding h.isClosedMap

theorem preimage_closure (h : X ‚âÉ‚Çú Y) (s : Set Y) : h ‚Åª¬π' closure s = closure (h ‚Åª¬π' s) :=
  h.isOpenMap.preimage_closure_eq_closure_preimage h.continuous _

theorem image_closure (h : X ‚âÉ‚Çú Y) (s : Set X) : h '' closure s = closure (h '' s) := by
  rw [‚Üê preimage_symm, preimage_closure]

theorem preimage_interior (h : X ‚âÉ‚Çú Y) (s : Set Y) : h ‚Åª¬π' interior s = interior (h ‚Åª¬π' s) :=
  h.isOpenMap.preimage_interior_eq_interior_preimage h.continuous _

theorem image_interior (h : X ‚âÉ‚Çú Y) (s : Set X) : h '' interior s = interior (h '' s) := by
  rw [‚Üê preimage_symm, preimage_interior]

theorem preimage_frontier (h : X ‚âÉ‚Çú Y) (s : Set Y) : h ‚Åª¬π' frontier s = frontier (h ‚Åª¬π' s) :=
  h.isOpenMap.preimage_frontier_eq_frontier_preimage h.continuous _

theorem image_frontier (h : X ‚âÉ‚Çú Y) (s : Set X) : h '' frontier s = frontier (h '' s) := by
  rw [‚Üê preimage_symm, preimage_frontier]

@[simp]
theorem comp_continuous_iff (h : X ‚âÉ‚Çú Y) {f : Z ‚Üí X} : Continuous (h ‚àò f) ‚Üî Continuous f :=
  h.isInducing.continuous_iff.symm

@[simp]
theorem comp_continuous_iff' (h : X ‚âÉ‚Çú Y) {f : Y ‚Üí Z} : Continuous (f ‚àò h) ‚Üî Continuous f :=
  h.isQuotientMap.continuous_iff.symm

theorem comp_continuousAt_iff (h : X ‚âÉ‚Çú Y) (f : Z ‚Üí X) (z : Z) :
    ContinuousAt (h ‚àò f) z ‚Üî ContinuousAt f z :=
  h.isInducing.continuousAt_iff.symm

theorem comp_continuousAt_iff' (h : X ‚âÉ‚Çú Y) (f : Y ‚Üí Z) (x : X) :
    ContinuousAt (f ‚àò h) x ‚Üî ContinuousAt f (h x) :=
  h.isInducing.continuousAt_iff' (by simp)

@[simp]
theorem comp_isOpenMap_iff (h : X ‚âÉ‚Çú Y) {f : Z ‚Üí X} : IsOpenMap (h ‚àò f) ‚Üî IsOpenMap f := by
  refine ‚ü®?_, fun hf => h.isOpenMap.comp hf‚ü©
  intro hf
  rw [‚Üê Function.id_comp f, ‚Üê h.symm_comp_self, Function.comp_assoc]
  exact h.symm.isOpenMap.comp hf

@[simp]
theorem comp_isOpenMap_iff' (h : X ‚âÉ‚Çú Y) {f : Y ‚Üí Z} : IsOpenMap (f ‚àò h) ‚Üî IsOpenMap f := by
  refine ‚ü®?_, fun hf => hf.comp h.isOpenMap‚ü©
  intro hf
  rw [‚Üê Function.comp_id f, ‚Üê h.self_comp_symm, ‚Üê Function.comp_assoc]
  exact hf.comp h.symm.isOpenMap

variable (X Y) in
/-- If both `X` and `Y` have a unique element, then `X ‚âÉ‚Çú Y`. -/
@[simps!]
def homeomorphOfUnique [Unique X] [Unique Y] : X ‚âÉ‚Çú Y :=
  { Equiv.ofUnique X Y with
    continuous_toFun := continuous_const
    continuous_invFun := continuous_const }

@[simp]
theorem map_nhds_eq (h : X ‚âÉ‚Çú Y) (x : X) : map h (ùìù x) = ùìù (h x) :=
  h.isEmbedding.map_nhds_of_mem _ (by simp)

theorem symm_map_nhds_eq (h : X ‚âÉ‚Çú Y) (x : X) : map h.symm (ùìù (h x)) = ùìù x := by
  rw [h.symm.map_nhds_eq, h.symm_apply_apply]

theorem nhds_eq_comap (h : X ‚âÉ‚Çú Y) (x : X) : ùìù x = comap h (ùìù (h x)) :=
  h.isInducing.nhds_eq_comap x

@[simp]
theorem comap_nhds_eq (h : X ‚âÉ‚Çú Y) (y : Y) : comap h (ùìù y) = ùìù (h.symm y) := by
  rw [h.nhds_eq_comap, h.apply_symm_apply]

end Homeomorph

namespace Equiv
variable {Z : Type*} [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]

/-- An equivalence between topological spaces respecting openness is a homeomorphism. -/
@[simps toEquiv]
def toHomeomorph (e : X ‚âÉ Y) (he : ‚àÄ s, IsOpen (e ‚Åª¬π' s) ‚Üî IsOpen s) : X ‚âÉ‚Çú Y where
  toEquiv := e
  continuous_toFun := continuous_def.2 fun _ ‚Ü¶ (he _).2
  continuous_invFun := continuous_def.2 fun s ‚Ü¶ by convert (he _).1; simp

@[simp] lemma coe_toHomeomorph (e : X ‚âÉ Y) (he) : ‚áë(e.toHomeomorph he) = e := rfl
lemma toHomeomorph_apply (e : X ‚âÉ Y) (he) (x : X) : e.toHomeomorph he x = e x := rfl

@[simp] lemma toHomeomorph_refl :
    (Equiv.refl X).toHomeomorph (fun _s ‚Ü¶ Iff.rfl) = Homeomorph.refl _ := rfl

@[simp] lemma toHomeomorph_symm (e : X ‚âÉ Y) (he) :
    (e.toHomeomorph he).symm = e.symm.toHomeomorph fun s ‚Ü¶ by convert (he _).symm; simp := rfl

lemma toHomeomorph_trans (e : X ‚âÉ Y) (f : Y ‚âÉ Z) (he hf) :
    (e.trans f).toHomeomorph (fun _s ‚Ü¶ (he _).trans (hf _)) =
    (e.toHomeomorph he).trans (f.toHomeomorph hf) := rfl

/-- An inducing equiv between topological spaces is a homeomorphism. -/
@[simps toEquiv]
def toHomeomorphOfIsInducing (f : X ‚âÉ Y) (hf : IsInducing f) : X ‚âÉ‚Çú Y :=
  { f with
    continuous_toFun := hf.continuous
    continuous_invFun := hf.continuous_iff.2 <| by simpa using continuous_id }

@[deprecated (since := "2024-10-28")] alias toHomeomorphOfInducing := toHomeomorphOfIsInducing

@[simp] lemma toHomeomorphOfIsInducing_apply (f : X ‚âÉ Y) (hf : IsInducing f) :
    ‚áë(f.toHomeomorphOfIsInducing hf) = f := rfl

@[simp] lemma toHomeomorphOfIsInducing_symm_apply (f : X ‚âÉ Y) (hf : IsInducing f) :
    ‚áë(f.toHomeomorphOfIsInducing hf).symm = f.symm := rfl

/-- If a bijective map `e : X ‚âÉ Y` is continuous and open, then it is a homeomorphism. -/
@[simps! toEquiv]
def toHomeomorphOfContinuousOpen (e : X ‚âÉ Y) (h‚ÇÅ : Continuous e) (h‚ÇÇ : IsOpenMap e) : X ‚âÉ‚Çú Y :=
  e.toHomeomorphOfIsInducing <|
    IsOpenEmbedding.of_continuous_injective_isOpenMap h‚ÇÅ e.injective h‚ÇÇ |>.toIsInducing

@[deprecated (since := "2025-04-16")]
alias _root_.Homeomorph.homeomorphOfContinuousOpen := toHomeomorphOfContinuousOpen

@[deprecated (since := "2025-04-16")]
alias _root_.Homeomorph.homeomorphOfContinuousOpen_toEquiv := toHomeomorphOfContinuousOpen_toEquiv

@[simp]
theorem toHomeomorphOfContinuousOpen_apply (e : X ‚âÉ Y) (h‚ÇÅ : Continuous e) (h‚ÇÇ : IsOpenMap e) :
    ‚áë(e.toHomeomorphOfContinuousOpen h‚ÇÅ h‚ÇÇ) = e := rfl

@[deprecated (since := "2025-04-16")]
alias _root_.Homeomorph.homeomorphOfContinuousOpen_apply := toHomeomorphOfContinuousOpen_apply

@[simp]
theorem toHomeomorphOfContinuousOpen_symm_apply (e : X ‚âÉ Y) (h‚ÇÅ : Continuous e) (h‚ÇÇ : IsOpenMap e) :
    ‚áë(e.toHomeomorphOfContinuousOpen h‚ÇÅ h‚ÇÇ).symm = e.symm := rfl

@[deprecated (since := "2025-04-16")]
alias _root_.Homeomorph.homeomorphOfContinuousOpen_symm_apply :=
  toHomeomorphOfContinuousOpen_symm_apply

/-- If a bijective map `e : X ‚âÉ Y` is continuous and open, then it is a homeomorphism. -/
@[simps! toEquiv]
def toHomeomorphOfContinuousClosed (e : X ‚âÉ Y) (h‚ÇÅ : Continuous e) (h‚ÇÇ : IsClosedMap e) : X ‚âÉ‚Çú Y :=
  e.toHomeomorphOfIsInducing <|
    IsClosedEmbedding.of_continuous_injective_isClosedMap h‚ÇÅ e.injective h‚ÇÇ |>.toIsInducing

@[deprecated (since := "2025-04-16")]
alias _root_.Homeomorph.homeomorphOfContinuousClosed := toHomeomorphOfContinuousClosed

@[simp]
theorem toHomeomorphOfContinuousClosed_apply (e : X ‚âÉ Y) (h‚ÇÅ : Continuous e) (h‚ÇÇ : IsClosedMap e) :
    ‚áë(e.toHomeomorphOfContinuousClosed h‚ÇÅ h‚ÇÇ) = e := rfl

@[simp]
theorem toHomeomorphOfContinuousClosed_symm_apply
    (e : X ‚âÉ Y) (h‚ÇÅ : Continuous e) (h‚ÇÇ : IsClosedMap e) :
    ‚áë(e.toHomeomorphOfContinuousClosed h‚ÇÅ h‚ÇÇ).symm = e.symm := rfl

end Equiv

/-- `HomeomorphClass F A B` states that `F` is a type of homeomorphisms. -/
class HomeomorphClass (F : Type*) (A B : outParam Type*)
    [TopologicalSpace A] [TopologicalSpace B] [h : EquivLike F A B] : Prop where
  map_continuous : ‚àÄ (f : F), Continuous f
  inv_continuous : ‚àÄ (f : F), Continuous (h.inv f)

namespace HomeomorphClass

variable {F Œ± Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [EquivLike F Œ± Œ≤]

/-- Turn an element of a type `F` satisfying `HomeomorphClass F Œ± Œ≤` into an actual
`Homeomorph`. This is declared as the default coercion from `F` to `Œ± ‚âÉ‚Çú Œ≤`. -/
@[coe]
def toHomeomorph [h : HomeomorphClass F Œ± Œ≤] (f : F) : Œ± ‚âÉ‚Çú Œ≤ :=
  { (f : Œ± ‚âÉ Œ≤) with
    continuous_toFun := h.map_continuous f
    continuous_invFun := h.inv_continuous f }

@[simp]
theorem coe_coe [h : HomeomorphClass F Œ± Œ≤] (f : F) : ‚áë(h.toHomeomorph f) = ‚áëf := rfl

instance [HomeomorphClass F Œ± Œ≤] : CoeOut F (Œ± ‚âÉ‚Çú Œ≤) :=
  ‚ü®HomeomorphClass.toHomeomorph‚ü©

theorem toHomeomorph_injective [HomeomorphClass F Œ± Œ≤] : Function.Injective ((‚Üë) : F ‚Üí Œ± ‚âÉ‚Çú Œ≤) :=
  fun _ _ e ‚Ü¶ DFunLike.ext _ _ fun a ‚Ü¶ congr_arg (fun e : Œ± ‚âÉ‚Çú Œ≤ ‚Ü¶ e.toFun a) e

instance [HomeomorphClass F Œ± Œ≤] : ContinuousMapClass F Œ± Œ≤ where
  map_continuous  f := map_continuous f

instance : HomeomorphClass (Œ± ‚âÉ‚Çú Œ≤) Œ± Œ≤ where
  map_continuous e := e.continuous_toFun
  inv_continuous e := e.continuous_invFun

end HomeomorphClass

section IsHomeomorph

variable [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z] {f : X ‚Üí Y}

/-- Predicate saying that `f` is a homeomorphism.

This should be used only when `f` is a concrete function whose continuous inverse is not easy to
write down. Otherwise, `Homeomorph` should be preferred as it bundles the continuous inverse.

Having both `Homeomorph` and `IsHomeomorph` is justified by the fact that so many function
properties are unbundled in the topology part of the library, and by the fact that a homeomorphism
is not merely a continuous bijection, that is `IsHomeomorph f` is not equivalent to
`Continuous f ‚àß Bijective f` but to `Continuous f ‚àß Bijective f ‚àß IsOpenMap f`. -/
structure IsHomeomorph (f : X ‚Üí Y) : Prop where
  continuous : Continuous f
  isOpenMap : IsOpenMap f
  bijective : Function.Bijective f

protected theorem Homeomorph.isHomeomorph (h : X ‚âÉ‚Çú Y) : IsHomeomorph h :=
  ‚ü®h.continuous, h.isOpenMap, h.bijective‚ü©

namespace IsHomeomorph

protected lemma injective (hf : IsHomeomorph f) : Function.Injective f := hf.bijective.injective
protected lemma surjective (hf : IsHomeomorph f) : Function.Surjective f := hf.bijective.surjective

protected lemma id : IsHomeomorph (@id X) := ‚ü®continuous_id, .id, Function.bijective_id‚ü©

lemma comp {g : Y ‚Üí Z} (hg : IsHomeomorph g) (hf : IsHomeomorph f) : IsHomeomorph (g ‚àò f) :=
  ‚ü®hg.1.comp hf.1, hg.2.comp hf.2, hg.3.comp hf.3‚ü©

end IsHomeomorph

end IsHomeomorph
