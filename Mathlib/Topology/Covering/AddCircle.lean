/-
Copyright (c) 2025 Junyan Xu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Junyan Xu
-/
module

public import Mathlib.Analysis.Normed.Module.Basic
public import Mathlib.Topology.Covering.Quotient
public import Mathlib.Topology.Instances.AddCircle.Defs

/-!
# Covering maps involving `AddCircle`

-/

@[expose] public section

namespace AddCircle

section AddCommGroup

open AddSubgroup

variable {ð•œ : Type*} [AddCommGroup ð•œ] (p : ð•œ) [TopologicalSpace ð•œ] [IsTopologicalAddGroup ð•œ]
  [DiscreteTopology (zmultiples p)]

theorem isAddQuotientCoveringMap_coe :
    IsAddQuotientCoveringMap ((â†‘) : ð•œ â†’ AddCircle p) (zmultiples p) :=
  isAddQuotientCoveringMap_of_comm _ DiscreteTopology.isDiscrete

theorem isCoveringMap_coe : IsCoveringMap ((â†‘) : ð•œ â†’ AddCircle p) :=
  (isAddQuotientCoveringMap_coe p).isCoveringMap

theorem isLocalHomeomorph_coe : IsLocalHomeomorph ((â†‘) : ð•œ â†’ AddCircle p) :=
  (isCoveringMap_coe p).isLocalHomeomorph

end AddCommGroup

section Field

open Topology

variable {ð•œ : Type*} [NormedField ð•œ] [NormedSpace â„š ð•œ] [LinearOrder ð•œ] [IsStrictOrderedRing ð•œ]
  (p : ð•œ)

open DistribMulAction

theorem isAddQuotientCoveringMap_zsmul {n : â„¤} (hn : n â‰  0) :
    IsAddQuotientCoveringMap (n â€¢ Â· : AddCircle p â†’ _) (toAddMonoidHom (AddCircle p) n).ker := by
  refine IsQuotientMap.isAddQuotientCoveringMap_of_isDiscrete_ker_addMonoidHom
    (f := toAddMonoidHom ..) ?_ (Set.Finite.isDiscrete ?_) rfl
  Â· /- To show that (n â€¢ Â·) on AddCircle p is a quotient map, it suffices to show
      its composition with â„ â†’ AddCircle p is a quotient map. -/
    apply IsQuotientMap.of_comp (f := ((â†‘) : ð•œ â†’ _)) continuous_quotient_mk' (continuous_zsmul n)
    /- This composition is equal to the composition with (n â€¢ Â·) on â„ (a homeomorphism)
      and the quotient map â„ â†’ AddCircle p. -/
    convert isQuotientMap_quotient_mk'.comp (affineHomeomorph (n : ð•œ) 0 (mod_cast hn)).isQuotientMap
    ext x
    simp_rw [Function.comp_apply, affineHomeomorph_apply, add_zero, â† zsmul_eq_mul]
    rfl
  rw [AddMonoidHom.coe_ker, Set.preimage, â† n.sign_mul_natAbs]
  simp_rw [toAddMonoidHom_apply, Set.mem_singleton_iff]
  obtain neg | pos := hn.lt_or_gt
  on_goal 1 => simp_rw [n.sign_eq_neg_one_of_neg neg, neg_mul, one_mul, neg_smul, neg_eq_zero]
  on_goal 2 => rw [n.sign_eq_one_of_pos pos, one_mul]
  all_goals simpa using finite_torsion p (n.natAbs_pos.mpr hn)

theorem isAddQuotientCoveringMap_nsmul {n : â„•} (hn : n â‰  0) :
    IsAddQuotientCoveringMap (n â€¢ Â· : AddCircle p â†’ _) (toAddMonoidHom (AddCircle p) n).ker := by
  convert isAddQuotientCoveringMap_zsmul p (n := n) (mod_cast hn)
  all_goals ext; simp

end Field

end AddCircle
