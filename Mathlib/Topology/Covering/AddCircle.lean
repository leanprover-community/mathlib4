/-
Copyright (c) 2025 Junyan Xu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Junyan Xu
-/
import Mathlib.Analysis.Normed.Module.Basic
import Mathlib.Topology.Covering.Galois
import Mathlib.Topology.Instances.AddCircle

/-!
# Covering maps involving `AddCircle`

-/

namespace AddCircle

section AddCommGroup

variable {ğ•œ : Type*} [AddCommGroup ğ•œ] (p : ğ•œ) [TopologicalSpace ğ•œ] [IsTopologicalAddGroup ğ•œ]
  [DiscreteTopology (AddSubgroup.zmultiples p)]

theorem isCoveringMap_coe : IsCoveringMap ((â†‘) : ğ•œ â†’ AddCircle p) := AddSubgroup.isCoveringMap _

theorem isLocalHomeomorph_coe : IsLocalHomeomorph ((â†‘) : ğ•œ â†’ AddCircle p) :=
  (isCoveringMap_coe p).isLocalHomeomorph

example (p : â„) : IsCoveringMap ((â†‘) : â„ â†’ AddCircle p) := isCoveringMap_coe p

end AddCommGroup

section Field

open Topology

variable {ğ•œ : Type*} [NormedField ğ•œ] [NormedSpace â„š ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
  (p : ğ•œ)

theorem isCoveringMap_zsmul {n : â„¤} (hn : n â‰  0) :
    IsCoveringMap fun x : AddCircle p â†¦ n â€¢ x := by
  apply IsQuotientMap.isCoveringMap_of_discrete_ker_addMonoidHom
    (f := DistribMulAction.toAddMonoidHom _ n)
  Â· /- To show that (n â€¢ Â·) on AddCircle p is a quotient map, it suffices to show
      its composition with â„ â†’ AddCircle p is a quotient map. -/
    apply IsQuotientMap.of_comp (f := ((â†‘) : ğ•œ â†’ _)) _ (continuous_zsmul n)
    Â· /- This composition is equal to the composition with (n â€¢ Â·) on â„ (a homeomorphism)
        and the quotient map â„ â†’ AddCircle p. -/
      convert isQuotientMap_quotient_mk'.comp
        (affineHomeomorph (n : ğ•œ) 0 <| by exact_mod_cast hn).isQuotientMap
      ext x; dsimp only [Function.comp_apply]
      rw [affineHomeomorph_apply, add_zero, â† zsmul_eq_mul]; rfl
    Â· exact continuous_quotient_mk'
  refine @Finite.instDiscreteTopology _ _ _ ?_
  simp_rw [AddMonoidHom.mem_ker, DistribMulAction.toAddMonoidHom_apply]
  rw [â† n.sign_mul_natAbs]
  obtain neg | pos := hn.lt_or_lt
  on_goal 1 => simp_rw [n.sign_eq_neg_one_of_neg neg, neg_mul, one_mul, neg_smul, neg_eq_zero]
  on_goal 2 => rw [n.sign_eq_one_of_pos pos, one_mul]
  all_goals simpa only [Nat.cast_smul_eq_nsmul] using
    Set.finite_coe_iff.mpr (finite_torsion p (n.natAbs_pos.mpr hn))

theorem isCoveringMap_nsmul {n : â„•} (hn : n â‰  0) :
    IsCoveringMap fun x : AddCircle p â†¦ n â€¢ x := by
  simpa using isCoveringMap_zsmul p (n := n) (by exact_mod_cast hn)

end Field

end AddCircle
