/-
Copyright (c) 2025 Junyan Xu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Junyan Xu
-/
module

public import Mathlib.Algebra.Algebra.Defs
public import Mathlib.Topology.Covering.Quotient
public import Mathlib.Topology.Instances.AddCircle.Defs

/-!
# Covering maps involving `AddCircle`

-/

public section

namespace AddCircle

section AddCommGroup

open AddSubgroup

variable {ð•œ : Type*} [AddCommGroup ð•œ] (p : ð•œ) [TopologicalSpace ð•œ] [IsTopologicalAddGroup ð•œ]
  [DiscreteTopology (zmultiples p)]

theorem isAddQuotientCoveringMap_coe :
    IsAddQuotientCoveringMap ((â†‘) : ð•œ â†’ AddCircle p) (zmultiples p) :=
  isAddQuotientCoveringMap_of_comm _ DiscreteTopology.isDiscrete

theorem isCoveringMap_coe : IsCoveringMap ((â†‘) : ð•œ â†’ AddCircle p) :=
  (isAddQuotientCoveringMap_coe p).isCoveringMap

theorem isLocalHomeomorph_coe : IsLocalHomeomorph ((â†‘) : ð•œ â†’ AddCircle p) :=
  (isCoveringMap_coe p).isLocalHomeomorph

end AddCommGroup

section Field

open Topology

variable {ð•œ : Type*} [TopologicalSpace ð•œ] [Ring ð•œ] [IsTopologicalRing ð•œ]
variable (p : ð•œ) [T0Space (AddCircle p)]
/- This instance can be supplied from:
- `[NormedSpace â„š ð•œ]` (with import `Mathlib.Analysis.Normed.Module.Basic`), or
- `[LinearOrder ð•œ] [IsOrderedMonoid ð•œ] [OrderTopology ð•œ]`
  (with import `Mathlib.Topology.Algebra.Order.ArchimedeanDiscrete`)
and `ð•œ := â„` satisfies both. -/

theorem isAddQuotientCoveringMap_zsmul {n : â„¤} (hn : IsUnit (n : ð•œ)) :
    IsAddQuotientCoveringMap (n â€¢ Â· : AddCircle p â†’ _)
      (zsmulAddGroupHom (Î± := AddCircle p) n).ker := by
  refine hn.isQuotientMap_zsmul (QuotientAddGroup.mk' _) isQuotientMap_quotient_mk'
    |>.isAddQuotientCoveringMap_of_isDiscrete_ker_addMonoidHom
    (f := zsmulAddGroupHom (Î± := AddCircle p) n)
    (Set.Finite.isDiscrete <| finite_torsion_of_isSMulRegular_int _ _ fun _ â†¦ ?_)
  simp_rw [zsmul_eq_mul]
  apply hn.isSMulRegular ð•œ

theorem isAddQuotientCoveringMap_nsmul {n : â„•} (hn : IsUnit (n : ð•œ)) :
    IsAddQuotientCoveringMap (n â€¢ Â· : AddCircle p â†’ _)
      (nsmulAddMonoidHom (Î± := AddCircle p) n).ker := by
  convert isAddQuotientCoveringMap_zsmul p (n := n) (mod_cast hn)
  all_goals ext; simp

theorem isAddQuotientCoveringMap_zsmul_of_ne_zero [Algebra â„š ð•œ] (n : â„¤) [NeZero n] :
    IsAddQuotientCoveringMap (n â€¢ Â· : AddCircle p â†’ _)
      (zsmulAddGroupHom (Î± := AddCircle p) n).ker :=
  isAddQuotientCoveringMap_zsmul p (n := n) <| by
    convert (Int.cast_ne_zero.mpr <| NeZero.ne n).isUnit.map (algebraMap â„š ð•œ); simp

theorem isAddQuotientCoveringMap_nsmul_of_ne_zero [Algebra â„š ð•œ] (n : â„•) [NeZero n] :
    IsAddQuotientCoveringMap (n â€¢ Â· : AddCircle p â†’ _)
      (nsmulAddMonoidHom (Î± := AddCircle p) n).ker :=
  isAddQuotientCoveringMap_nsmul p (n := n) <| by
    convert (Nat.cast_ne_zero.mpr <| NeZero.ne n).isUnit.map (algebraMap â„š ð•œ); simp

end Field

end AddCircle
