/-
Copyright (c) 2025 Vincent Beffara. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Vincent Beffara
-/
import Mathlib.Tactic.Peel
import Mathlib.Topology.ContinuousMap.Interval
import Mathlib.Topology.Covering.Basic
import Mathlib.Topology.UnitInterval

/-!
# Lifting through covering maps

This path proves the existence of path and homotopy lifts through a covering map.

## Main definitions

Let `f : E â†’ X` be a covering map. The main theorems in the file are:
* `exists_unique_lift`: Given a path `Î³ : C(I, X)` starting at `f e`, there is a unique lift `Î“ :
  C(I, E)` starting at `e` such that `f âˆ˜ Î“ = Î³`.
* `exists_unique_hlift`: Given a continuous map `Î“â‚€ : C(Y, X)` and a homotopy `Î³ : C(I Ã— Y, X)`
  satisfying `Î³ (0, â¬) = f âˆ˜ Î“â‚€`, there is a unique homotopy `Î“ : C(I Ã— Y, E)` satisfying `Î“ (0, â¬)
  = Î“â‚€` and `f âˆ˜ Î“ = Î³`.
* `exists_unique_hlift'`: Specialized version of `exists_unique_hlift` lifting from `C(I, C(Y, X))`
  to `C(I, C(Y, E))` when `Y` is locally compact.
In addition:
* `lift hf Î³ he`: Given a path `Î³ : C(I, X)` starting at `f e`, this is the lift of `Î³` starting at
  `e`.
-/

open Set Topology unitInterval Filter ContinuousMap

local instance : Fact ((0 : â„) â‰¤ 1) := âŸ¨zero_le_oneâŸ©

variable {E X Y Z : Type*}
  [TopologicalSpace E] [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]
  {f : E â†’ X} (hf : IsCoveringMap f) {e eâ‚€ : E} {x xâ‚€ : X} {Î³ : C(I, X)} {m n : â„•}

namespace IsCoveringMap

/-- Subdivision of an interval with an associated sequence of trivializations of the covering `p`.
  One can lift a path `Î³` by gluing local lifts along such a subdivision if it is adapted to it,
  see `fits`. -/
structure LiftSetup (p : E â†’ X) where
  /-- The bounds of the intervals in the subdivision. -/
  t : â„• â†’ â„
  /-- The number of (non-trivial) pieces. -/
  n : â„•
  /-- The sequence of basepoints for the trivializations. -/
  c : â„• â†’ X
  /-- The trivializations. -/
  T : âˆ€ i, Trivialization (p â»Â¹' {c i}) p
  ht : Monotone t
  ht0 : t 0 = 0
  ht1 : âˆ€ m â‰¥ n, t m = 1

variable {S : LiftSetup f}

local instance : Fact (S.t 0 â‰¤ S.t n) := âŸ¨S.ht n.zero_leâŸ©

local instance : Fact (S.t n â‰¤ S.t (n + 1)) := âŸ¨S.ht n.le_succâŸ©

namespace LiftSetup

/-- The `n`th interval in the partition contained in `S`. -/
abbrev icc (S : LiftSetup f) (n : â„•) : Set â„ := Icc (S.t n) (S.t (n + 1))

theorem htn : S.t S.n = 1 := S.ht1 S.n le_rfl

theorem t_mem_I : S.t n âˆˆ I := by
  refine âŸ¨?_, ?_âŸ© <;> simp only [â† S.ht0, â† S.ht1 (n + S.n) (by omega)] <;> apply S.ht <;> omega

@[simp]
theorem icc_subset_I : Icc (S.t m) (S.t n) âŠ† I := by
  rintro t âŸ¨ht0, ht1âŸ© ; exact âŸ¨le_trans t_mem_I.1 ht0, le_trans ht1 t_mem_I.2âŸ©

attribute [simp] ht0 ht1

/-- The embedding of intervals adapted to the partition in `S` into the unit interval. -/
def embed (S : LiftSetup f) (m n : â„•) : C(Icc (S.t m) (S.t n), I) :=
  âŸ¨fun t => âŸ¨t, icc_subset_I t.2âŸ©, by fun_propâŸ©

/-- This holds if the path `Î³` maps each interval in the partition in `S` to the base set of the
corresponding trivialization. -/
def fits (S : LiftSetup f) (Î³ : C(I, X)) : Prop :=
  âˆ€ n âˆˆ Finset.range S.n, MapsTo (IccExtendCM Î³) (S.icc n) (S.T n).baseSet

theorem fits.eventually {yâ‚€ : Y} {Î³ : C(Y, C(I, X))} (hS : S.fits (Î³ yâ‚€)) :
    âˆ€á¶  y in ğ“ yâ‚€, S.fits (Î³ y) := by
  simp only [LiftSetup.fits, eventually_all_finset] at hS âŠ¢
  peel hS with n hn hS
  have key := ContinuousMap.eventually_mapsTo CompactIccSpace.isCompact_Icc (S.T n).open_baseSet hS
  have h4 := IccExtendCM.2.tendsto (Î³ yâ‚€) |>.eventually key
  exact Î³.2.tendsto yâ‚€ |>.eventually h4

theorem exist (hf : IsCoveringMap f) (Î³ : C(I, X)) : âˆƒ S : LiftSetup f, S.fits Î³ := by
  choose T hT using fun t => (hf (Î³ t)).2
  let V t : Set I := Î³ â»Â¹' (T t).baseSet
  have h1 t : IsOpen (V t) := (T t).open_baseSet.preimage Î³.continuous
  have h2 : univ âŠ† â‹ƒ t, V t := fun t _ => mem_iUnion.2 âŸ¨t, hT tâŸ©
  choose t ht0 ht ht1 c hc using exists_monotone_Icc_subset_open_cover_unitInterval h1 h2
  obtain âŸ¨n, ht1âŸ© := ht1
  use âŸ¨(t Â·), n, Î³ âˆ˜ c, fun n => T (c n), ht, by simpa using ht0, by simpa using ht1âŸ©
  rintro k - s hs
  simpa [icc_subset_I hs] using hc k hs

/-- This gathers a path which is adapted to a `LiftSetup` and a point in the fiber above its
starting point. -/
private abbrev Liftable (S : LiftSetup f) := {Î³e : C(I, X) Ã— E // S.fits Î³e.1 âˆ§ f Î³e.2 = Î³e.1 0}

private noncomputable def partial_lift (S : LiftSetup f) : âˆ€ n â‰¤ S.n,
    {F : C(S.Liftable, C(Icc (S.t 0) (S.t n), E)) // âˆ€ Î³e,
      F Î³e âŠ¥ = Î³e.1.2 âˆ§ âˆ€ t, f (F Î³e t) = Î³e.1.1 (S.embed _ _ t)}
  | 0 => fun _ => by
    use ContinuousMap.const'.comp âŸ¨fun ye => ye.1.2, by fun_propâŸ©
    rintro âŸ¨âŸ¨Î³, eâŸ©, -, h2âŸ©
    refine âŸ¨rfl, ?_âŸ©
    simp only [Subtype.forall, LiftSetup.ht0, Icc_self, mem_singleton_iff]
    rintro t rfl
    exact h2
  | n + 1 => fun hn => by
    obtain âŸ¨Î¦, hÎ¦âŸ© := S.partial_lift n (by omega)
    replace hn : n âˆˆ Finset.range S.n := by simp ; omega
    refine âŸ¨?_, ?_âŸ©
    Â· refine (concatCM (b := S.t n)).comp âŸ¨?_, ?_âŸ©
      Â· intro Î³e
        have h5 : f (Î¦ Î³e âŠ¤) = Î³e.1.1 âŸ¨S.t n, _âŸ© := (hÎ¦ Î³e).2 âŠ¤
        have h6 : S.t n âˆˆ S.icc n := by simpa using S.ht n.le_succ
        let left : C(â†‘(Icc (S.t 0) (S.t n)), E) := Î¦ Î³e
        let next : C(S.icc n, E) := by
          let Î³n : C(S.icc n, (S.T n).baseSet) := by
            refine âŸ¨fun t => âŸ¨Î³e.1.1 (S.embed _ _ t), ?_âŸ©, ?_âŸ©
            Â· simpa [icc_subset_I t.2] using Î³e.2.1 n hn t.2
            Â· fun_prop
          refine .comp âŸ¨_, continuous_subtype_valâŸ© <| (S.T n).clift (âŸ¨left âŠ¤, ?_âŸ©, Î³n)
          simpa [Trivialization.mem_source, left, h5, icc_subset_I h6] using Î³e.2.1 n hn h6
        use âŸ¨left, nextâŸ©
        simp only [comp_apply, coe_mk, next]
        rw [Trivialization.clift_self]
        simp only [hÎ¦, left, next]
        rfl
      Â· refine Continuous.subtype_mk (continuous_prod_mk.2 âŸ¨by fun_prop, ?_âŸ©) _
        apply ContinuousMap.continuous_postcomp _ |>.comp
        apply (S.T n).clift.continuous.comp
        refine continuous_prod_mk.2 âŸ¨?_, ?_âŸ©
        Â· exact (continuous_eval_const _).comp Î¦.continuous |>.subtype_mk _
        Â· let Î¨ : C(S.Liftable Ã— S.icc n, C(I, X) Ã— I) :=
            âŸ¨fun fx => (fx.1.1.1, âŸ¨fx.2.1, icc_subset_I fx.2.2âŸ©), by fun_propâŸ©
          let Î¦ : C(S.Liftable Ã— S.icc n, (S.T n).baseSet) := by
            refine âŸ¨fun fx => âŸ¨fx.1.1.1 âŸ¨fx.2.1, icc_subset_I fx.2.2âŸ©, ?_âŸ©, ?_âŸ©
            Â· obtain âŸ¨_, _âŸ© := icc_subset_I fx.2.2
              have := fx.1.2.1 n hn fx.2.2
              rw [IccExtendCM_of_mem] at this ; assumption
            Â· apply Continuous.subtype_mk
              exact ContinuousEval.continuous_eval.comp Î¨.continuous
          exact Î¦.curry.continuous
    Â· rintro âŸ¨âŸ¨Î³, eâŸ©, hÎ³, heâŸ©
      dsimp
      constructor
      Â· rw [concatCM_left (S.ht n.zero_le)]
        exact hÎ¦ âŸ¨âŸ¨Î³, eâŸ©, hÎ³, heâŸ© |>.1
      Â· rintro âŸ¨t, htâŸ©
        by_cases htn : t â‰¤ S.t n
        Â· rw [concatCM_left htn] ; exact hÎ¦ âŸ¨âŸ¨Î³, eâŸ©, hÎ³, heâŸ© |>.2 âŸ¨t, _âŸ©
        Â· rw [concatCM_right <| le_of_not_le htn]
          simp only [comp_apply, coe_mk, Trivialization.proj_clift (proj := f)]
          rfl

/-- Lifting paths through a `LiftSetup`, as a bundled continuous map from paths adapted to the
setup. -/
noncomputable def lift (S : LiftSetup f) :
    {F : C(S.Liftable, C(I, E)) // âˆ€ Î³e, F Î³e 0 = Î³e.1.2 âˆ§ âˆ€ t, f (F Î³e t) = Î³e.1.1 t} := by
  obtain âŸ¨F, hFâŸ© := S.partial_lift S.n le_rfl
  let Î¦ : C(I, Icc (S.t 0) (S.t S.n)) := âŸ¨fun t => âŸ¨t, by simpâŸ©, by fun_propâŸ©
  refine âŸ¨âŸ¨fun Î³e => (F Î³e).comp Î¦, by fun_propâŸ©, fun Î³e => âŸ¨?_, fun t => ?_âŸ©âŸ©
  Â· simpa [Bot.bot] using hF Î³e |>.1
  Â· simpa [embed] using hF Î³e |>.2 (Î¦ t)

end LiftSetup

include hf

theorem exists_unique_lift (he : f e = Î³ 0) : âˆƒ! Î“ : C(I, E), Î“ 0 = e âˆ§ f âˆ˜ Î“ = Î³ := by
  obtain âŸ¨S, hSâŸ© := LiftSetup.exist hf Î³
  obtain âŸ¨F, hFâŸ© := S.lift
  have h1 : F âŸ¨âŸ¨Î³, eâŸ©, hS, heâŸ© 0 = e := hF âŸ¨âŸ¨Î³, eâŸ©, hS, heâŸ© |>.1
  have h2 : f âˆ˜ F âŸ¨âŸ¨Î³, eâŸ©, hS, heâŸ© = Î³ := by ext t ; exact hF âŸ¨âŸ¨Î³, eâŸ©, hS, heâŸ© |>.2 t
  refine âŸ¨F âŸ¨âŸ¨Î³, eâŸ©, hS, heâŸ©, âŸ¨h1, h2âŸ©, ?_âŸ©
  rintro Î“ âŸ¨rfl, hÎ“â‚‚âŸ©
  apply coe_injective
  apply hf.eq_of_comp_eq (a := 0) Î“.continuous (ContinuousMap.continuous _) <;> simp [*]

/-- The path obtained by lifting through a covering map. -/
noncomputable def lift (Î³ : C(I, X)) (he : f e = Î³ 0) : C(I, E) :=
  (hf.exists_unique_lift he).choose

@[simp]
theorem lift_left (Î³ : C(I, X)) (he : f e = Î³ 0) : hf.lift Î³ he 0 = e :=
  (hf.exists_unique_lift he).choose_spec.1.1

@[simp]
theorem lift_comp (Î³ : C(I, X)) (he : f e = Î³ 0) : f âˆ˜ hf.lift Î³ he = Î³ :=
  (hf.exists_unique_lift he).choose_spec.1.2

theorem lift_unique (Î³ : C(I, X)) (he : f e = Î³ 0) {Î“ : C(I, E)} (hÎ“ : Î“ 0 = e âˆ§ f âˆ˜ Î“ = Î³) :
    Î“ = hf.lift Î³ he :=
  (hf.exists_unique_lift he).unique hÎ“ âŸ¨hf.lift_left Î³ he, hf.lift_comp Î³ heâŸ©

variable {Y : Type*} [TopologicalSpace Y] {Î³ : C(I Ã— Y, X)} {Î“â‚€ : C(Y, E)}

private def slice (Î³ : C(I Ã— Y, X)) : C(Y, C(I, X)) := Î³.comp prodSwap |>.curry

noncomputable def joint_lift (hÎ“â‚€ : âˆ€ y, f (Î“â‚€ y) = Î³ (0, y)) : C(Y, C(I, E)) := by
  use fun y => hf.lift (slice Î³ y) (hÎ“â‚€ y)
  rw [continuous_iff_continuousAt]
  intro yâ‚€
  obtain âŸ¨S, hSâŸ© := LiftSetup.exist hf (slice Î³ yâ‚€)
  apply ContinuousOn.continuousAt ?_ hS.eventually
  rw [continuousOn_iff_continuous_restrict]
  obtain âŸ¨Gâ‚, hGâ‚âŸ© := S.lift
  let Gâ‚‚ : C({y // S.fits (slice Î³ y)}, S.Liftable) :=
    âŸ¨fun y => âŸ¨âŸ¨slice Î³ y, Î“â‚€ yâŸ©, y.2, hÎ“â‚€ yâŸ©, by fun_propâŸ©
  convert Gâ‚.comp Gâ‚‚ |>.continuous
  ext1 y
  have h3 := hGâ‚ âŸ¨âŸ¨slice Î³ y, Î“â‚€ yâŸ©, y.2, hÎ“â‚€ yâŸ©
  apply coe_injective
  apply hf.eq_of_comp_eq (a := 0) (ContinuousMap.continuous _) (ContinuousMap.continuous _)
  Â· simp [Gâ‚‚, h3]
  Â· simp only [Set.restrict_apply, mem_setOf_eq, hf.lift_left, hf.lift_comp, comp_apply, coe_mk, Gâ‚‚]
    ext t
    simp [h3]

noncomputable def hlift (hÎ“â‚€ : âˆ€ y, f (Î“â‚€ y) = Î³ (0, y)) : C(I Ã— Y, E) :=
  (hf.joint_lift hÎ“â‚€).uncurry |>.comp prodSwap

theorem hlift_left (hÎ“â‚€ : âˆ€ y, f (Î“â‚€ y) = Î³ (0, y)) : (hf.hlift hÎ“â‚€ âŸ¨0, Â·âŸ©) = Î“â‚€ :=
  funext fun _ => hf.lift_left _ _

theorem hlift_comp (hÎ“â‚€ : âˆ€ y, f (Î“â‚€ y) = Î³ (0, y)) : f âˆ˜ hf.hlift hÎ“â‚€ = Î³ :=
  funext fun âŸ¨t, yâŸ© => congr_fun (hf.lift_comp (slice Î³ y) (hÎ“â‚€ y)) t

theorem hlift_congr {Î“ : I Ã— Y â†’ E} (g_lifts : f âˆ˜ Î“ = Î³) (cont_0 : Continuous (Î“ âŸ¨0, Â·âŸ©))
    (cont_A : âˆ€ a, Continuous (Î“ âŸ¨Â·, aâŸ©)) :
    Î“ = hf.hlift (Î“â‚€ := âŸ¨(Î“ âŸ¨0, Â·âŸ©), cont_0âŸ©) (congr_fun g_lifts âŸ¨0, Â·âŸ©) := by
  ext âŸ¨t, yâŸ©
  have h1 : f âˆ˜ (Î“ âŸ¨Â·, yâŸ©) = slice Î³ y := funext (congr_fun g_lifts âŸ¨Â·, yâŸ©)
  have h2 := hf.lift_unique (slice Î³ y) (congr_fun g_lifts âŸ¨0, yâŸ©) (Î“ := âŸ¨_, cont_A yâŸ©) âŸ¨rfl, h1âŸ©
  exact ContinuousMap.congr_fun h2 t

theorem hlift_unique (hÎ“â‚€ : âˆ€ y, f (Î“â‚€ y) = Î³ (0, y)) {Î“ : C(I Ã— Y, E)} (h1 : (Î“ âŸ¨0, Â·âŸ©) = Î“â‚€)
    (h2 : f âˆ˜ Î“ = Î³) : Î“ = hf.hlift hÎ“â‚€ := by
  convert coe_injective <| hf.hlift_congr h2 (Î“.curry 0 |>.continuous) (slice Î“ Â· |>.continuous)
  simp [congr_fun h1]

theorem exists_unique_hlift (hÎ“â‚€ : âˆ€ y, f (Î“â‚€ y) = Î³ (0, y)) :
    âˆƒ! Î“ : C(I Ã— Y, E), (Î“ âŸ¨0, Â·âŸ©) = Î“â‚€ âˆ§ f âˆ˜ Î“ = Î³ :=
  âŸ¨hf.hlift hÎ“â‚€, âŸ¨hf.hlift_left hÎ“â‚€, hf.hlift_comp hÎ“â‚€âŸ©, fun _ h => hf.hlift_unique hÎ“â‚€ h.1 h.2âŸ©

/-- Specialized version of `exists_unique_hlift` stated in terms of a continuous map from the unit
interval to `C(Y, X)` in the case when `Y` is locally compact. -/
theorem exists_unique_hlift' [LocallyCompactSpace Y] {Î³ : C(I, C(Y, X))}
    (hÎ“â‚€ : âˆ€ y, f (Î“â‚€ y) = Î³ 0 y) :
    âˆƒ! Î“ : C(I, C(Y, E)), Î“ 0 = Î“â‚€ âˆ§ âˆ€ y, f âˆ˜ (Î“ Â· y) = (Î³ Â· y) := by
  obtain âŸ¨Î“, h1, h2âŸ© := exists_unique_hlift hf hÎ“â‚€ (Î³ := Î³.uncurry)
  refine âŸ¨Î“.curry, âŸ¨?_, ?_âŸ©, ?_âŸ©
  Â· exact coe_injective h1.1
  Â· exact fun y => funext (congr_fun h1.2 âŸ¨Â·, yâŸ©)
  Â· intro Î“' h3
    have h4 : f âˆ˜ â‡‘Î“'.uncurry = â‡‘Î³.uncurry := funext fun âŸ¨t, yâŸ© => congr_fun (h3.2 y) t
    have h5 : Î“'.uncurry = Î“ := by simpa [h3, h4] using h2 Î“'.uncurry
    ext t y
    exact ContinuousMap.congr_fun h5 (t, y)

theorem continuous_lift (Î³ : C(I Ã— Y, X)) {Î“ : I Ã— Y â†’ E} (g_lifts : f âˆ˜ Î“ = Î³)
    (cont_0 : Continuous (Î“ âŸ¨0, Â·âŸ©)) (cont_A : âˆ€ a, Continuous (Î“ âŸ¨Â·, aâŸ©)) : Continuous Î“ := by
  rw [hf.hlift_congr g_lifts cont_0 cont_A]
  exact ContinuousMap.continuous _

end IsCoveringMap
