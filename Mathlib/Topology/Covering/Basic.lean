/-
Copyright (c) 2022 Thomas Browning. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Thomas Browning
-/
module

public import Mathlib.Topology.DiscreteSubset
public import Mathlib.Topology.FiberBundle.Basic
public import Mathlib.Topology.IsLocalHomeomorph

/-!
# Covering Maps

This file defines covering maps.

## Main definitions

* `IsEvenlyCovered f x I`: A point `x` is evenly covered by `f : E ‚Üí X` with fiber `I` if `I` is
  discrete and there is a homeomorphism `f ‚Åª¬π' U ‚âÉ‚Çú U √ó I` for some open set `U` containing `x`
  with `f ‚Åª¬π' U` open, such that the induced map `f ‚Åª¬π' U ‚Üí U` coincides with `f`.
* `IsCoveringMap f`: A function `f : E ‚Üí X` is a covering map if every point `x` is evenly
  covered by `f` with fiber `f ‚Åª¬π' {x}`. The fibers `f ‚Åª¬π' {x}` must be discrete, but if `X` is
  not connected, then the fibers `f ‚Åª¬π' {x}` are not necessarily isomorphic. Also, `f` is not
  assumed to be surjective, so the fibers are even allowed to be empty.
-/

@[expose] public section

open Bundle Topology

variable {E X : Type*} [TopologicalSpace E] [TopologicalSpace X] (f : E ‚Üí X) (s : Set X)

/-- A point `x : X` is evenly covered by `f : E ‚Üí X` if `x` has an evenly covered neighborhood.

**Remark**: `DiscreteTopology I ‚àß ‚àÉ Trivialization I f, x ‚àà t.baseSet` would be a simpler
definition, but unfortunately it does not work if `E` is nonempty but nonetheless `f` has empty
fibers over `s`. If `OpenPartialHomeomorph` could be refactored to work with an empty space and a
nonempty space while preserving the APIs, we could switch back to the definition. -/
def IsEvenlyCovered (x : X) (I : Type*) [TopologicalSpace I] :=
  DiscreteTopology I ‚àß ‚àÉ U : Set X, x ‚àà U ‚àß IsOpen U ‚àß IsOpen (f ‚Åª¬π' U) ‚àß
    ‚àÉ H : f ‚Åª¬π' U ‚âÉ‚Çú U √ó I, ‚àÄ x, (H x).1.1 = f x

namespace IsEvenlyCovered

variable {f} {I : Type*} [TopologicalSpace I]

/-- If `x : X` is evenly covered by `f` with fiber `I`, then `I` is homeomorphic to `f ‚Åª¬π' {x}`. -/
noncomputable def fiberHomeomorph {x : X} (h : IsEvenlyCovered f x I) : I ‚âÉ‚Çú f ‚Åª¬π' {x} := by
  choose _ U hxU hU hfU H hH using h
  exact
  { toFun i := ‚ü®H.symm (‚ü®x, hxU‚ü©, i), by simp [‚Üê hH]‚ü©
    invFun e := (H ‚ü®e, by rwa [Set.mem_preimage, (e.2 : f e = x)]‚ü©).2
    left_inv _ := by simp
    right_inv e := Set.inclusion_injective (Set.preimage_mono (Set.singleton_subset_iff.mpr hxU)) <|
      H.injective <| Prod.ext (Subtype.ext <| by simpa [hH] using e.2.symm) (by simp)
    continuous_toFun := by fun_prop
    continuous_invFun := by fun_prop }

theorem discreteTopology_fiber {x : X} (h : IsEvenlyCovered f x I) : DiscreteTopology (f ‚Åª¬π' {x}) :=
  have := h.1; h.fiberHomeomorph.discreteTopology

/-- If `x` is evenly covered by `f` with nonempty fiber `I`, then we can construct a
trivialization of `f` at `x` with fiber `I`. -/
noncomputable def toTrivialization' {x : X} [Nonempty I] (h : IsEvenlyCovered f x I) :
    Trivialization I f := by
  choose _ U hxU hU hfU H hH using h
  classical exact
  { toFun e := if he : f e ‚àà U then ‚ü®(H ‚ü®e, he‚ü©).1, (H ‚ü®e, he‚ü©).2‚ü© else ‚ü®x, Classical.arbitrary I‚ü©
    invFun xi := H.symm (if hx : xi.1 ‚àà U then ‚ü®xi.1, hx‚ü© else ‚ü®x, hxU‚ü©, xi.2)
    source := f ‚Åª¬π' U
    target := U √óÀ¢ Set.univ
    map_source' e (he : f e ‚àà U) := by simp [he]
    map_target' _ _ := Subtype.coe_prop _
    left_inv' e (he : f e ‚àà U) := by simp [he]
    right_inv' xi := by rintro ‚ü®hx, -‚ü©; simpa [hx] using fun h ‚Ü¶ (h (H.symm _).2).elim
    open_source := hfU
    open_target := hU.prod isOpen_univ
    continuousOn_toFun := continuousOn_iff_continuous_restrict.mpr <|
      ((continuous_subtype_val.prodMap continuous_id).comp H.continuous).congr
      fun ‚ü®e, (he : f e ‚àà U)‚ü© ‚Ü¶ by simp [Prod.map, he]
    continuousOn_invFun := continuousOn_iff_continuous_restrict.mpr <|
      ((continuous_subtype_val.comp H.symm.continuous).comp (by fun_prop :
        Continuous fun ui ‚Ü¶ ‚ü®‚ü®_, ui.2.1‚ü©, ui.1.2‚ü©)).congr fun ‚ü®‚ü®x, i‚ü©, ‚ü®hx, _‚ü©‚ü© ‚Ü¶ by simp [hx]
    baseSet := U
    open_baseSet := hU
    source_eq := rfl
    target_eq := rfl
    proj_toFun e (he : f e ‚àà U) := by simp [he, hH] }

/-- If `x` is evenly covered by `f`, then we can construct a trivialization of `f` at `x`. -/
noncomputable def toTrivialization {x : X} [Nonempty I] (h : IsEvenlyCovered f x I) :
    Trivialization (f ‚Åª¬π' {x}) f :=
  h.toTrivialization'.transFiberHomeomorph h.fiberHomeomorph

theorem mem_toTrivialization_baseSet {x : X} [Nonempty I] (h : IsEvenlyCovered f x I) :
    x ‚àà h.toTrivialization.baseSet := h.2.choose_spec.1

theorem toTrivialization_apply {x : E} [Nonempty I] (h : IsEvenlyCovered f (f x) I) :
    (h.toTrivialization x).2 = ‚ü®x, rfl‚ü© :=
  h.fiberHomeomorph.symm.injective <| by
    simp [toTrivialization, toTrivialization', dif_pos h.2.choose_spec.1, fiberHomeomorph]

protected theorem continuousAt {x : E} (h : IsEvenlyCovered f (f x) I) : ContinuousAt f x :=
  have ‚ü®_, _, hxU, _, _, H, _‚ü© := h
  have : Nonempty I := ‚ü®(H ‚ü®x, hxU‚ü©).2‚ü©
  let e := h.toTrivialization
  e.continuousAt_proj (e.mem_source.mpr (mem_toTrivialization_baseSet h))

theorem of_fiber_homeomorph {J} [TopologicalSpace J] (g : I ‚âÉ‚Çú J) {x : X}
    (h : IsEvenlyCovered f x I) : IsEvenlyCovered f x J :=
  have ‚ü®inst, U, hxU, hU, hfU, H, hH‚ü© := h
  ‚ü®g.discreteTopology, U, hxU, hU, hfU, H.trans (.prodCongr (.refl U) g), fun _ ‚Ü¶ by simp [hH]‚ü©

theorem to_isEvenlyCovered_preimage {x : X} (h : IsEvenlyCovered f x I) :
    IsEvenlyCovered f x (f ‚Åª¬π' {x}) :=
  h.of_fiber_homeomorph h.fiberHomeomorph

theorem of_trivialization [DiscreteTopology I] {x : X} {t : Trivialization I f}
    (hx : x ‚àà t.baseSet) : IsEvenlyCovered f x I :=
  ‚ü®‚Äπ_‚Ä∫, _, hx, t.open_baseSet, t.source_eq ‚ñ∏ t.open_source,
  { toFun e := ‚ü®‚ü®f e, e.2‚ü©, (t e).2‚ü©
    invFun xi := ‚ü®t.invFun (xi.1, xi.2), by
      rw [Set.mem_preimage, ‚Üê t.mem_source]; exact t.map_target (t.target_eq ‚ñ∏ ‚ü®xi.1.2, ‚ü®‚ü©‚ü©)‚ü©
    left_inv e := Subtype.ext <| t.symm_apply_mk_proj (t.mem_source.mpr e.2)
    right_inv xi := by simp [t.proj_symm_apply', t.apply_symm_apply']
    continuous_toFun := (IsInducing.subtypeVal.prodMap .id).continuous_iff.mpr <|
      (continuousOn_iff_continuous_restrict.mp <| t.continuousOn_toFun.mono t.source_eq.ge).congr
      fun e ‚Ü¶ by simp [t.mk_proj_snd' e.2]
    continuous_invFun := IsInducing.subtypeVal.continuous_iff.mpr <|
      t.continuousOn_invFun.comp_continuous (continuous_subtype_val.prodMap continuous_id)
      fun ‚ü®x, _‚ü© ‚Ü¶ t.target_eq ‚ñ∏ ‚ü®x.2, ‚ü®‚ü©‚ü© }, fun _ ‚Ü¶ by simp‚ü©

variable (I) in
theorem of_preimage_eq_empty [IsEmpty I] {x : X} {U : Set X} (hUx : U ‚àà ùìù x) (hfU : f ‚Åª¬π' U = ‚àÖ) :
    IsEvenlyCovered f x I :=
  have ‚ü®V, hVU, hV, hxV‚ü© := mem_nhds_iff.mp hUx
  have hfV : f ‚Åª¬π' V = ‚àÖ := Set.eq_empty_of_subset_empty ((Set.preimage_mono hVU).trans hfU.le)
  have := Set.isEmpty_coe_sort.mpr hfV
  ‚ü®inferInstance, _, hxV, hV, hfV ‚ñ∏ isOpen_empty, .empty, isEmptyElim‚ü©

theorem restrictPreimage {x : X} (hxs : x ‚àà s) (h : IsEvenlyCovered f x I) :
    IsEvenlyCovered (s.restrictPreimage f) ‚ü®x, hxs‚ü© I :=
  have ‚ü®inst, U, hxU, hU, hfU, H, hH‚ü© := h
  ‚ü®inst, Subtype.val ‚Åª¬π' U, hxU, hU.preimage (by fun_prop), hfU.preimage continuous_subtype_val,
    { toFun e := (‚ü®‚ü®(H ‚ü®e, e.2‚ü©).1, hH _ ‚ñ∏ e.1.2‚ü©, by simpa only [hH] using e.2‚ü©, (H ‚ü®e, e.2‚ü©).2)
      invFun x := ‚ü®‚ü®H.symm (‚ü®x.1, x.1.2‚ü©, x.2), by simp [‚Üê hH]‚ü©, by simp [‚Üê hH]‚ü©
      left_inv _ := by simp, right_inv _ := by simp }, fun _ ‚Ü¶ by ext; apply hH‚ü©

theorem subtypeVal_comp (hs : IsOpen s) {x : s} {f : E ‚Üí s} (h : IsEvenlyCovered f x I) :
    IsEvenlyCovered (Subtype.val ‚àò f) x I :=
  have ‚ü®inst, U, hxU, hU, hfU, H, hH‚ü© := h
  have : Subtype.val ‚àò f ‚Åª¬π' (Subtype.val '' U) = f ‚Åª¬π' U := by ext; simp
  ‚ü®inst, Subtype.val '' U, ‚ü®x, hxU, rfl‚ü©, hs.isOpenMap_subtype_val _ hU, by rwa [this], .trans
    (.setCongr this) (H.trans <| .prodCongr (IsEmbedding.subtypeVal.homeomorphImage U) (.refl I)),
    fun _ ‚Ü¶ congr_arg Subtype.val (hH _)‚ü©

theorem comp_subtypeVal (hs : IsOpen s) (hfs : IsOpen (f ‚Åª¬π' s)) {x : X} (hx : x ‚àà s)
    (h : IsEvenlyCovered (fun e : f ‚Åª¬π' s ‚Ü¶ f e) x I) : IsEvenlyCovered f x I :=
  have ‚ü®inst, U, hxU, hU, hfU, H, hH‚ü© := h
  (isEmpty_or_nonempty I).elim (fun _ ‚Ü¶ .of_preimage_eq_empty _ ((hs.inter hU).mem_nhds ‚ü®hx, hxU‚ü©)
    <| Set.not_nonempty_iff_eq_empty.mp fun ‚ü®e, he‚ü© ‚Ü¶ isEmptyElim (H ‚ü®‚ü®e, he.1‚ü©, he.2‚ü©).2) fun _ ‚Ü¶
  have hUs : U ‚äÜ s := fun y hy ‚Ü¶ by
    convert Set.mem_preimage.mp (H.symm (‚ü®y, hy‚ü©, Classical.arbitrary I)).1.2; simp [‚Üê hH]
  have : Subtype.val '' ((fun e : f ‚Åª¬π' s ‚Ü¶ f e) ‚Åª¬π' U) = f ‚Åª¬π' U := by ext; simpa using @hUs _
  ‚ü®inst, U, hxU, hU, this ‚ñ∏ hfs.isOpenMap_subtype_val _ hfU, .trans (.symm <| .trans
    (IsEmbedding.subtypeVal.homeomorphImage _) <| .setCongr this) H, fun x ‚Ü¶ by
    dsimp; convert hH ‚ü®‚ü®x, hUs x.2‚ü©, x.2‚ü© using 4; exact (Equiv.symm_apply_eq _).mpr rfl‚ü©

theorem comp_homeomorph {x : X} (h : IsEvenlyCovered f x I) {E'} [TopologicalSpace E']
    (g : E' ‚âÉ‚Çú E) : IsEvenlyCovered (f ‚àò g) x I :=
  have ‚ü®inst, U, hxU, hU, hfU, H, hH‚ü© := h
  ‚ü®inst, U, hxU, hU, hfU.preimage g.continuous, .trans (.trans
    (.setCongr <| by rw [Set.preimage_comp, g.image_symm]) (g.symm.image _).symm) H, fun _ ‚Ü¶ hH _‚ü©

@[simp] theorem comp_homeomorph_iff {x : X} {E'} [TopologicalSpace E'] (g : E' ‚âÉ‚Çú E) :
    IsEvenlyCovered (f ‚àò g) x I ‚Üî IsEvenlyCovered f x I where
  mp h := by convert h.comp_homeomorph g.symm; ext; simp
  mpr h := h.comp_homeomorph g

theorem homeomorph_comp {x : X} (h : IsEvenlyCovered f x I) {Y} [TopologicalSpace Y] (g : X ‚âÉ‚Çú Y) :
    IsEvenlyCovered (g ‚àò f) (g x) I :=
  have ‚ü®inst, U, hxU, hU, hfU, H, hH‚ü© := h
  ‚ü®inst, g '' U, ‚ü®x, hxU, rfl‚ü©, g.isOpen_image.mpr hU, by simpa [Set.preimage_comp],
    .trans (.setCongr <| by simp [Set.preimage_comp]) (H.trans <| (g.image U).prodCongr (.refl I)),
    fun _ ‚Ü¶ congr_arg g (hH _)‚ü©

@[simp] theorem homeomorph_comp_iff {x : X} {Y} [TopologicalSpace Y] (g : X ‚âÉ‚Çú Y) :
    IsEvenlyCovered (g ‚àò f) (g x) I ‚Üî IsEvenlyCovered f x I where
  mp h := by convert h.homeomorph_comp g.symm <;> ((try ext); simp)
  mpr h := h.homeomorph_comp g

end IsEvenlyCovered

/-- A covering map is a continuous function `f : E ‚Üí X` with discrete fibers such that each point
  of `X` has an evenly covered neighborhood. -/
def IsCoveringMapOn :=
  ‚àÄ x ‚àà s, IsEvenlyCovered f x (f ‚Åª¬π' {x})

namespace IsCoveringMapOn

theorem of_isEmpty [IsEmpty E] : IsCoveringMapOn f s := fun _ _ ‚Ü¶ .to_isEvenlyCovered_preimage
  (.of_preimage_eq_empty Empty Filter.univ_mem <| Set.eq_empty_of_isEmpty _)

/-- A constructor for `IsCoveringMapOn` when there are both empty and nonempty fibers. -/
theorem mk' (F : s ‚Üí Type*) [‚àÄ x : s, TopologicalSpace (F x)] [hF : ‚àÄ x : s, DiscreteTopology (F x)]
    (t : ‚àÄ x : s, x.1 ‚àà Set.range f ‚Üí {t : Trivialization (F x) f // x.1 ‚àà t.baseSet})
    (h : ‚àÄ x : s, x.1 ‚àâ Set.range f ‚Üí ‚àÉ U ‚àà ùìù x.1, f ‚Åª¬π' U = ‚àÖ) :
    IsCoveringMapOn f s := fun x hx ‚Ü¶ by
  lift x to s using hx
  by_cases hxf : x.1 ‚àà Set.range f
  ¬∑ exact .to_isEvenlyCovered_preimage (.of_trivialization (t x hxf).2)
  ¬∑ have ‚ü®U, hUx, hfU‚ü© := h x hxf
    exact .to_isEvenlyCovered_preimage (.of_preimage_eq_empty Empty hUx hfU)

theorem mk (F : s ‚Üí Type*) [‚àÄ x, TopologicalSpace (F x)] [hF : ‚àÄ x, DiscreteTopology (F x)]
    (e : ‚àÄ x, Trivialization (F x) f) (h : ‚àÄ x, x.1 ‚àà (e x).baseSet) :
    IsCoveringMapOn f s := by
  cases isEmpty_or_nonempty E
  ¬∑ exact .of_isEmpty _ _
  refine .mk' _ _ _ (fun x _ ‚Ü¶ ‚ü®e x, h x‚ü©) fun x hx ‚Ü¶ (hx ?_).elim
  exact ‚ü®(e x).invFun (x, (e x <| Classical.arbitrary E).2), (e x).proj_symm_apply' (h x)‚ü©

variable {f s}

theorem mono {t : Set X} (hf : IsCoveringMapOn f s) (ht : t ‚äÜ s) : IsCoveringMapOn f t :=
  fun x hx ‚Ü¶ hf x (ht hx)

protected theorem continuousAt (hf : IsCoveringMapOn f s) {x : E} (hx : f x ‚àà s) :
    ContinuousAt f x := (hf (f x) hx).continuousAt

protected theorem continuousOn (hf : IsCoveringMapOn f s) : ContinuousOn f (f ‚Åª¬π' s) :=
  continuousOn_of_forall_continuousAt fun _ ‚Ü¶ hf.continuousAt

protected theorem isLocalHomeomorphOn (hf : IsCoveringMapOn f s) :
    IsLocalHomeomorphOn f (f ‚Åª¬π' s) := by
  refine IsLocalHomeomorphOn.mk f (f ‚Åª¬π' s) fun x hx ‚Ü¶ ?_
  have : Nonempty (f ‚Åª¬π' {f x}) := ‚ü®‚ü®x, rfl‚ü©‚ü©
  let e := (hf (f x) hx).toTrivialization
  have h := (hf (f x) hx).mem_toTrivialization_baseSet
  let he := e.mem_source.2 h
  refine
    ‚ü®e.toOpenPartialHomeomorph.trans
        { toFun := fun p => p.1
          invFun := fun p => ‚ü®p, x, rfl‚ü©
          source := e.baseSet √óÀ¢ ({‚ü®x, rfl‚ü©} : Set (f ‚Åª¬π' {f x}))
          target := e.baseSet
          open_source :=
            e.open_baseSet.prod (discreteTopology_iff_isOpen_singleton.1 (hf (f x) hx).1 ‚ü®x, rfl‚ü©)
          open_target := e.open_baseSet
          map_source' := fun p => And.left
          map_target' := fun p hp => ‚ü®hp, rfl‚ü©
          left_inv' := fun p hp => Prod.ext rfl hp.2.symm
          right_inv' := fun p _ => rfl
          continuousOn_toFun := continuousOn_fst
          continuousOn_invFun := by fun_prop },
      ‚ü®he, by rwa [e.toOpenPartialHomeomorph.symm_symm, e.proj_toFun x he],
        (hf (f x) hx).toTrivialization_apply‚ü©,
      fun p h => (e.proj_toFun p h.1).symm‚ü©

theorem restrictPreimage (hf : IsCoveringMapOn f s) (t : Set X) :
    IsCoveringMapOn (t.restrictPreimage f) (Subtype.val ‚Åª¬π' s) :=
  fun x hs ‚Ü¶ ((hf x hs).restrictPreimage t x.2).to_isEvenlyCovered_preimage

theorem comp_homeomorph (hf : IsCoveringMapOn f s) {E'} [TopologicalSpace E'] (g : E' ‚âÉ‚Çú E) :
    IsCoveringMapOn (f ‚àò g) s :=
  fun x hx ‚Ü¶ ((hf x hx).comp_homeomorph _).to_isEvenlyCovered_preimage

@[simp] theorem comp_homeomorph_iff {E'} [TopologicalSpace E'] (g : E' ‚âÉ‚Çú E) :
    IsCoveringMapOn (f ‚àò g) s ‚Üî IsCoveringMapOn f s where
  mp h := by convert h.comp_homeomorph g.symm; ext; simp
  mpr h := h.comp_homeomorph g

theorem homeomorph_comp (hf : IsCoveringMapOn f s) {Y} [TopologicalSpace Y] (g : X ‚âÉ‚Çú Y) :
    IsCoveringMapOn (g ‚àò f) (g.symm ‚Åª¬π' s) :=
  fun y hy ‚Ü¶ (g.apply_symm_apply y ‚ñ∏ (hf _ hy).homeomorph_comp _).to_isEvenlyCovered_preimage

@[simp] theorem homeomorph_comp_iff {Y} [TopologicalSpace Y] (g : X ‚âÉ‚Çú Y) :
    IsCoveringMapOn (g ‚àò f) (g.symm ‚Åª¬π' s) ‚Üî IsCoveringMapOn f s where
  mp h := by convert h.homeomorph_comp g.symm <;> (ext; simp)
  mpr h := h.homeomorph_comp g

end IsCoveringMapOn

/-- A covering map is a continuous function `f : E ‚Üí X` with discrete fibers such that each point
  of `X` has an evenly covered neighborhood. -/
def IsCoveringMap :=
  ‚àÄ x, IsEvenlyCovered f x (f ‚Åª¬π' {x})

variable {f}

theorem isCoveringMap_iff_isCoveringMapOn_univ : IsCoveringMap f ‚Üî IsCoveringMapOn f .univ := by
  simp only [IsCoveringMap, IsCoveringMapOn, Set.mem_univ, forall_true_left]

protected theorem IsCoveringMap.isCoveringMapOn (hf : IsCoveringMap f) : IsCoveringMapOn f .univ :=
  isCoveringMap_iff_isCoveringMapOn_univ.mp hf

theorem IsCoveringMapOn.isCoveringMap_restrictPreimage (hf : IsCoveringMapOn f s) :
    IsCoveringMap (s.restrictPreimage f) :=
  isCoveringMap_iff_isCoveringMapOn_univ.mpr <| by simpa using hf.restrictPreimage s

theorem IsCoveringMapOn.of_isCoveringMap_restrictPreimage (hs : IsOpen s) (hfs : IsOpen (f ‚Åª¬π' s))
    (hf : IsCoveringMap (s.restrictPreimage f)) : IsCoveringMapOn f s := fun x hx ‚Ü¶
  (((hf ‚ü®x, hx‚ü©).subtypeVal_comp _ hs).comp_subtypeVal _ hs hfs hx).to_isEvenlyCovered_preimage

variable (f)

namespace IsCoveringMap

theorem of_isEmpty [IsEmpty E] : IsCoveringMap f :=
  isCoveringMap_iff_isCoveringMapOn_univ.mpr <| .of_isEmpty _ _

theorem of_discreteTopology [DiscreteTopology E] [DiscreteTopology X] : IsCoveringMap f :=
  fun x ‚Ü¶ ‚ü®inferInstance, {x}, rfl, isOpen_discrete _, isOpen_discrete _,
    { toFun e := ‚ü®‚ü®x, rfl‚ü©, e‚ü©
      invFun xi := xi.2
      left_inv _ := rfl
      right_inv _ := Prod.ext (Subsingleton.elim ..) rfl },
    (¬∑.2.symm)‚ü©

/-- A constructor for `IsCoveringMap` when there are both empty and nonempty fibers. -/
theorem mk' (F : X ‚Üí Type*) [‚àÄ x, TopologicalSpace (F x)] [‚àÄ x, DiscreteTopology (F x)]
    (t : ‚àÄ x, x ‚àà Set.range f ‚Üí {t : Trivialization (F x) f // x ‚àà t.baseSet})
    (h : IsClosed (Set.range f)) : IsCoveringMap f :=
  isCoveringMap_iff_isCoveringMapOn_univ.mpr <| .mk' f _ _ (fun x h ‚Ü¶ t x h) fun _x hx ‚Ü¶
    ‚ü®_, h.isOpen_compl.mem_nhds hx, Set.eq_empty_of_forall_notMem fun x h ‚Ü¶ h ‚ü®x, rfl‚ü©‚ü©

theorem mk (F : X ‚Üí Type*) [‚àÄ x, TopologicalSpace (F x)] [‚àÄ x, DiscreteTopology (F x)]
    (e : ‚àÄ x, Trivialization (F x) f) (h : ‚àÄ x, x ‚àà (e x).baseSet) : IsCoveringMap f :=
  isCoveringMap_iff_isCoveringMapOn_univ.mpr <| .mk _ _ _ _ fun x ‚Ü¶ h x

variable {f}
variable (hf : IsCoveringMap f)
include hf

protected theorem continuous : Continuous f :=
  continuousOn_univ.mp hf.isCoveringMapOn.continuousOn

protected theorem isLocalHomeomorph : IsLocalHomeomorph f :=
  isLocalHomeomorph_iff_isLocalHomeomorphOn_univ.mpr hf.isCoveringMapOn.isLocalHomeomorphOn

protected theorem isOpenMap : IsOpenMap f :=
  hf.isLocalHomeomorph.isOpenMap

theorem isQuotientMap (hf' : Function.Surjective f) : IsQuotientMap f :=
  hf.isOpenMap.isQuotientMap hf.continuous hf'

protected theorem isSeparatedMap : IsSeparatedMap f :=
  fun e‚ÇÅ e‚ÇÇ he hne ‚Ü¶ by
    have : Nonempty (f ‚Åª¬π' {f e‚ÇÅ}) := ‚ü®‚ü®e‚ÇÅ, rfl‚ü©‚ü©
    specialize hf (f e‚ÇÅ)
    let t := hf.toTrivialization
    have := hf.discreteTopology_fiber
    have he‚ÇÅ := hf.mem_toTrivialization_baseSet
    have he‚ÇÇ := he‚ÇÅ; simp_rw [he] at he‚ÇÇ; rw [‚Üê t.mem_source] at he‚ÇÅ he‚ÇÇ
    refine ‚ü®t.source ‚à© (Prod.snd ‚àò t) ‚Åª¬π' {(t e‚ÇÅ).2}, t.source ‚à© (Prod.snd ‚àò t) ‚Åª¬π' {(t e‚ÇÇ).2},
      ?_, ?_, ‚ü®he‚ÇÅ, rfl‚ü©, ‚ü®he‚ÇÇ, rfl‚ü©, Set.disjoint_left.mpr fun x h‚ÇÅ h‚ÇÇ ‚Ü¶ hne (t.injOn he‚ÇÅ he‚ÇÇ ?_)‚ü©
    iterate 2
      exact t.continuousOn_toFun.isOpen_inter_preimage t.open_source
        (continuous_snd.isOpen_preimage _ <| isOpen_discrete _)
    refine Prod.ext ?_ (h‚ÇÅ.2.symm.trans h‚ÇÇ.2)
    rwa [t.proj_toFun e‚ÇÅ he‚ÇÅ, t.proj_toFun e‚ÇÇ he‚ÇÇ]

variable {A} [TopologicalSpace A] {s : Set A} {g g‚ÇÅ g‚ÇÇ : A ‚Üí E}

/-- Proposition 1.34 of [hatcher02]. -/
theorem eq_of_comp_eq [PreconnectedSpace A] (h‚ÇÅ : Continuous g‚ÇÅ) (h‚ÇÇ : Continuous g‚ÇÇ)
    (he : f ‚àò g‚ÇÅ = f ‚àò g‚ÇÇ) (a : A) (ha : g‚ÇÅ a = g‚ÇÇ a) : g‚ÇÅ = g‚ÇÇ :=
  hf.isSeparatedMap.eq_of_comp_eq hf.isLocalHomeomorph.isLocallyInjective h‚ÇÅ h‚ÇÇ he a ha

theorem const_of_comp [PreconnectedSpace A] (cont : Continuous g)
    (he : ‚àÄ a a', f (g a) = f (g a')) (a a') : g a = g a' :=
  hf.isSeparatedMap.const_of_comp hf.isLocalHomeomorph.isLocallyInjective cont he a a'

theorem eqOn_of_comp_eqOn (hs : IsPreconnected s) (h‚ÇÅ : ContinuousOn g‚ÇÅ s) (h‚ÇÇ : ContinuousOn g‚ÇÇ s)
    (he : s.EqOn (f ‚àò g‚ÇÅ) (f ‚àò g‚ÇÇ)) {a : A} (has : a ‚àà s) (ha : g‚ÇÅ a = g‚ÇÇ a) : s.EqOn g‚ÇÅ g‚ÇÇ :=
  hf.isSeparatedMap.eqOn_of_comp_eqOn hf.isLocalHomeomorph.isLocallyInjective hs h‚ÇÅ h‚ÇÇ he has ha

theorem constOn_of_comp (hs : IsPreconnected s) (cont : ContinuousOn g s)
    (he : ‚àÄ a ‚àà s, ‚àÄ a' ‚àà s, f (g a) = f (g a'))
    {a a'} (ha : a ‚àà s) (ha' : a' ‚àà s) : g a = g a' :=
  hf.isSeparatedMap.constOn_of_comp hf.isLocalHomeomorph.isLocallyInjective hs cont he ha ha'

theorem restrictPreimage (t : Set X) : IsCoveringMap (t.restrictPreimage f) := by
  rw [isCoveringMap_iff_isCoveringMapOn_univ] at hf ‚ä¢
  exact hf.restrictPreimage t

theorem comp_homeomorph {E'} [TopologicalSpace E'] (g : E' ‚âÉ‚Çú E) : IsCoveringMap (f ‚àò g) := by
  rw [isCoveringMap_iff_isCoveringMapOn_univ] at hf ‚ä¢
  exact hf.comp_homeomorph g

theorem homeomorph_comp {Y} [TopologicalSpace Y] (g : X ‚âÉ‚Çú Y) : IsCoveringMap (g ‚àò f) := by
  rw [isCoveringMap_iff_isCoveringMapOn_univ] at hf ‚ä¢
  exact hf.homeomorph_comp g

omit hf

theorem comp_homeomorph_iff {E'} [TopologicalSpace E'] (g : E' ‚âÉ‚Çú E) :
    IsCoveringMap (f ‚àò g) ‚Üî IsCoveringMap f where
  mp h := by convert h.comp_homeomorph g.symm; ext; simp
  mpr h := h.comp_homeomorph g

theorem homeomorph_comp_iff {Y} [TopologicalSpace Y] (g : X ‚âÉ‚Çú Y) :
    IsCoveringMap (g ‚àò f) ‚Üî IsCoveringMap f where
  mp h := by convert h.homeomorph_comp g.symm; ext; simp
  mpr h := h.homeomorph_comp g

end IsCoveringMap

theorem IsCoveringMapOn.of_isCoveringMap_subtype {s : Set X} (hs : IsOpen s) {f : E ‚Üí X}
    (h : ‚àÄ x, f x ‚àà s) (hf : IsCoveringMap fun x ‚Ü¶ (‚ü®f x, h x‚ü© : s)) : IsCoveringMapOn f s :=
  have eq : f ‚Åª¬π' s = .univ := by simpa [Set.range, Set.subset_def] using h
  of_isCoveringMap_restrictPreimage _ hs (by simp [eq]) <|
    hf.comp_homeomorph ((Homeomorph.setCongr eq).trans (Homeomorph.Set.univ E))

variable {f}

protected theorem IsFiberBundle.isCoveringMap {F : Type*} [TopologicalSpace F] [DiscreteTopology F]
    (hf : ‚àÄ x : X, ‚àÉ e : Trivialization F f, x ‚àà e.baseSet) : IsCoveringMap f :=
  IsCoveringMap.mk f (fun _ => F) (fun x => Classical.choose (hf x)) fun x =>
    Classical.choose_spec (hf x)

protected theorem FiberBundle.isCoveringMap {F : Type*} {E : X ‚Üí Type*} [TopologicalSpace F]
    [DiscreteTopology F] [TopologicalSpace (Bundle.TotalSpace F E)] [‚àÄ x, TopologicalSpace (E x)]
    [FiberBundle F E] : IsCoveringMap (œÄ F E) :=
  IsFiberBundle.isCoveringMap fun x => ‚ü®trivializationAt F E x, mem_baseSet_trivializationAt F E x‚ü©

open Function in
/-- Let `f : E ‚Üí X` be a (not necessarily continuous) map between topological spaces, and let
`V` be an open subset of `X`. Suppose that there is a family `U` of disjoint subsets of `E`
that covers `f‚Åª¬π(V)` such that for every `i`,

 1. `f` is injective on `U·µ¢`,
 2. `V` is contained in the image `f(U·µ¢)`,
 3. the open sets in `V` are determined by their preimages in `U·µ¢`.

Then `f` admits a `Trivialization` over the base set `V`. -/
@[simps source target baseSet] noncomputable def IsOpen.trivializationDiscrete [Nonempty (X ‚Üí E)]
    {Œπ} [Nonempty Œπ] [TopologicalSpace Œπ] [DiscreteTopology Œπ] (U : Œπ ‚Üí Set E) (V : Set X)
    (open_V : IsOpen V) (open_iff : ‚àÄ i {W}, W ‚äÜ V ‚Üí (IsOpen W ‚Üî IsOpen (f ‚Åª¬π' W ‚à© U i)))
    (inj : ‚àÄ i, (U i).InjOn f) (surj : ‚àÄ i, (U i).SurjOn f V)
    (disjoint : Pairwise (Disjoint on U)) (exhaustive : f ‚Åª¬π' V ‚äÜ ‚ãÉ i, U i) :
    Trivialization Œπ f := by
  have exhaustive' := exhaustive
  simp_rw [Set.subset_def, Set.mem_iUnion] at exhaustive
  choose idx idx_U using exhaustive
  choose inv inv_U f_inv using surj
  classical
  let F : PartialEquiv E (X √ó Œπ) :=
  { toFun e := (f e, if he : f e ‚àà V then idx e he else Classical.arbitrary Œπ),
    invFun x := if hx : x.1 ‚àà V then inv x.2 hx else Classical.arbitrary (X ‚Üí E) x.1,
    source := f ‚Åª¬π' V,
    target := V √óÀ¢ Set.univ,
    map_source' x hx := ‚ü®hx, ‚ü®‚ü©‚ü©
    map_target' x hx := by rw [dif_pos hx.1]; apply (f_inv _ hx.1).symm ‚ñ∏ hx.1,
    left_inv' e he := by
      simp_rw [dif_pos (id he : f e ‚àà V)]
      exact inj _ (inv_U _ he) (idx_U e he) (f_inv _ _)
    right_inv' x hx := by
      rw [dif_pos hx.1]
      refine Prod.ext (f_inv _ hx.1) ?_
      rw [dif_pos ((f_inv _ hx.1).symm ‚ñ∏ hx.1)]
      by_contra h; exact (disjoint h).le_bot ‚ü®idx_U .., inv_U _ _‚ü© }
  have open_preim {W} (hWV : W ‚äÜ V) (open_W : IsOpen W) : IsOpen (f ‚Åª¬π' W) := by
    convert isOpen_iUnion (fun i ‚Ü¶ (open_iff i hWV).mp open_W)
    rw [‚Üê Set.inter_iUnion, eq_comm, Set.inter_eq_left]
    exact (Set.preimage_mono hWV).trans exhaustive'
  have open_source : IsOpen F.source := open_preim subset_rfl open_V
  have cont_f : ContinuousOn f F.source := (continuousOn_open_iff open_source).mpr
    fun W open_W ‚Ü¶ open_preim Set.inter_subset_left (open_V.inter open_W)
  refine
  { toPartialEquiv := F,
    open_source := open_source,
    open_target := open_V.prod isOpen_univ,
    continuousOn_toFun := cont_f.prodMk <| continuousOn_of_forall_continuousAt fun e he ‚Ü¶
      continuous_const (y := idx e he) |>.continuousAt.congr <| mem_nhds_iff.mpr
        ‚ü®U (idx e he) ‚à© F.source, fun e' he' ‚Ü¶ ?_, ?_, idx_U e he, he‚ü©
    continuousOn_invFun := continuousOn_prod_of_discrete_right.mpr fun i ‚Ü¶ ?_,
    baseSet := V,
    open_baseSet := open_V,
    source_eq := rfl,
    target_eq := rfl,
    proj_toFun _ _ := rfl }
  ¬∑ by_contra h; apply (disjoint h).le_bot
    ¬∑ dsimp only; rw [dif_pos (by exact he'.2)]; exact ‚ü®he'.1, idx_U ..‚ü©
  ¬∑ rwa [Set.inter_comm, ‚Üê open_iff _ subset_rfl]
  ¬∑ simp_rw [F, Set.prodMk_mem_set_prod_eq, Set.mem_univ, and_true]
    refine (continuousOn_open_iff open_V).mpr fun W open_W ‚Ü¶ ?_
    rw [open_iff i Set.inter_subset_left]
    convert ((open_iff i subset_rfl).mp open_V).inter open_W using 1
    refine Set.ext fun e ‚Ü¶ and_right_comm.trans (and_congr_right fun ‚ü®hV, hU‚ü© ‚Ü¶ ?_)
    rw [Set.mem_preimage, dif_pos hV, inj i (inv_U i _) hU (f_inv i _)]

variable {s}

variable (f) in
theorem IsDiscrete.of_openPartialHomeomorph {t : Set E} {x : X}
    (htx : t ‚äÜ f ‚Åª¬π' {x}) (hf : ‚àÄ e ‚àà t, ‚àÉ œÜ : OpenPartialHomeomorph E X, e ‚àà œÜ.source ‚àß œÜ = f) :
    IsDiscrete t :=
  isDiscrete_iff_forall_exists_isOpen.mpr fun e he ‚Ü¶ by
    obtain ‚ü®œÜ, hœÜ, rfl‚ü© := hf e he
    exact ‚ü®_, œÜ.open_source, subset_antisymm (fun e' he' ‚Ü¶ œÜ.injOn he'.1 hœÜ <|
      (htx he'.2).trans (htx he).symm) <| Set.singleton_subset_iff.mpr ‚ü®hœÜ, he‚ü©‚ü©

open Set in
/-- If `f : E ‚Üí X` is a closed map between topological spaces with `E` Hausdorff, such that the
fiber over a point `x : X` is finite and `f` restricts to a homeomorphism on a neighborhood of
every point of the fiber, then `x` admits an evenly covered neighborhood. -/
theorem IsClosedMap.isEvenlyCovered_of_openPartialHomeomorph [T2Space E] {x : X}
    (hf : IsClosedMap f) (fin : (f ‚Åª¬π' {x}).Finite)
    (h : ‚àÄ e ‚àà f ‚Åª¬π' {x}, ‚àÉ œÜ : OpenPartialHomeomorph E X, e ‚àà œÜ.source ‚àß œÜ = f) :
    IsEvenlyCovered f x (f ‚Åª¬π' {x}) := by
  have : DiscreteTopology (f ‚Åª¬π' {x}) :=
    (IsDiscrete.of_openPartialHomeomorph f subset_rfl h).1
  /- for each preimage e of x, choose a homeomorphism œÜ‚Çë
    from a neighborhood of e to its image -/
  choose œÜ hœÜ using fun e : f ‚Åª¬π' {x} ‚Ü¶ h e e.2
  -- separately, choose pairwise disjoint neighborhoods V‚Çë by Hausdorff-ness
  have ‚ü®V, hV, disj‚ü© := fin.t2_separation
  -- let V‚Çë' be the intersection V‚Çë ‚à© dom(œÜ‚Çë)
  let V' (e : f ‚Åª¬π' {x}) := V e ‚à© (œÜ e).source
  have hV' e : IsOpen (V' e) := (hV e).2.inter (œÜ e).open_source
  have : ‚ãÉ e, V' e ‚àà nhdsSet (f ‚Åª¬π' {x}) :=
    (isOpen_iUnion hV').mem_nhdsSet.2 fun e he ‚Ü¶ mem_iUnion_of_mem ‚ü®e, he‚ü© ‚ü®(hV e).1, (hœÜ _).1‚ü©
  -- since f is a closed map, the union of the V‚Çë' contains the preimage of a neighborhood U of x
  have ‚ü®W, hWx, hWV‚ü© := isClosedMap_iff_comap_nhds_le.mp hf this
  cases isEmpty_or_nonempty (f ‚Åª¬π' {x})
  ¬∑ exact .of_preimage_eq_empty _ hWx (by simpa using hWV)
  have ‚ü®U, hUW, hU, hxU‚ü© := mem_nhds_iff.mp hWx
  -- show that the intersection of U with the images of V‚Çë' is evenly covered
  let U' := U ‚à© ‚ãÇ e : f ‚Åª¬π' {x}, f '' (V' e)
  have : Finite (f ‚Åª¬π' {x}) := fin
  have hU' : IsOpen U' := hU.inter <| isOpen_iInter_of_finite fun e ‚Ü¶ by
    convert ‚Üê (œÜ e).isOpen_image_of_subset_source (hV' _) inter_subset_right; exact (hœÜ e).2
  have hUV e : U' ‚äÜ f '' V' e := inter_subset_right.trans (iInter_subset ..)
  have : Nonempty E := ‚ü®Classical.arbitrary (f ‚Åª¬π' {x})‚ü©
  refine .of_trivialization (t := hU'.trivializationDiscrete _ _
    (fun e s hs ‚Ü¶ ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ ?_‚ü©) (fun e ‚Ü¶ ?_)
    (fun e ‚Ü¶ .mono subset_rfl (hUV e) (surjOn_image f _))
    (pairwise_disjoint_mono disj.subtype fun e ‚Ü¶ inter_subset_left)
    ((preimage_mono (inter_subset_left.trans hUW)).trans hWV))
    ‚ü®hxU, Set.mem_iInter.mpr fun e ‚Ü¶ ‚ü®e, ‚ü®(hV e).1, (hœÜ e).1‚ü©, e.2‚ü©‚ü©
  ¬∑ convert ((œÜ e).isOpen_inter_preimage h).inter (hV e).2 using 1
    simp_rw [(hœÜ e).2, V']; ac_rfl
  ¬∑ have : s ‚äÜ (œÜ e).target := hs.trans <| (hUV e).trans <| by
      rw [‚Üê (œÜ e).image_source_eq_target, (hœÜ e).2]; exact image_mono inter_subset_right
    rw [‚Üê (œÜ e).isOpen_symm_image_iff_of_subset_target this,
      (œÜ e).symm_image_eq_source_inter_preimage this, (hœÜ e).2, inter_comm]
    convert h using 1
    refine inter_eq_inter_iff_left.mpr ‚ü®fun e' h ‚Ü¶ h.2.2, fun e' h ‚Ü¶ ‚ü®?_ , h.2‚ü©‚ü©
    have ‚ü®e'', ‚ü®_, mem‚ü©, eq‚ü© := mem_iInter.mp (hs h.1).2 e
    rwa [‚Üê (œÜ e).injOn mem h.2 (by rwa [(hœÜ e).2])]
  ¬∑ convert ‚Üê (œÜ e).injOn.mono inter_subset_right; exact (hœÜ e).2

/-- If `f : E ‚Üí X` is a closed map between topological spaces with `E` Hausdorff, and `s` is
a subset of `X` on which `f` has finite fibers, such that `f` restricts to a homeomorphism on
a neighborhood of every point of `f ‚Åª¬π' s`, then `f` is a covering map on `s`. -/
theorem IsClosedMap.isCoveringMapOn_of_openPartialHomeomorph [T2Space E]
    (hf : IsClosedMap f) (hs : ‚àÄ x ‚àà s, (f ‚Åª¬π' {x}).Finite)
    (h : ‚àÄ e ‚àà f ‚Åª¬π' s, ‚àÉ œÜ : OpenPartialHomeomorph E X, e ‚àà œÜ.source ‚àß œÜ = f) :
    IsCoveringMapOn f s :=
  fun x hx ‚Ü¶ hf.isEvenlyCovered_of_openPartialHomeomorph (hs x hx) fun e he ‚Ü¶ h e (by apply he ‚ñ∏ hx)

/-- If `f : E ‚Üí X` is a continuous map between Hausdorff spaces with `E` compact,
and `f` restricts to a homeomorphism on a neighborhood of every point of a fiber `f ‚Åª¬π' {x}`,
then `x` admits an evenly covered neighborhood. -/
theorem IsEvenlyCovered.of_openPartialHomeomorph
    [T2Space E] [T2Space X] [CompactSpace E] {x : X} (hf : Continuous f)
    (h : ‚àÄ e ‚àà f ‚Åª¬π' {x}, ‚àÉ œÜ : OpenPartialHomeomorph E X, e ‚àà œÜ.source ‚àß œÜ = f) :
    IsEvenlyCovered f x (f ‚Åª¬π' {x}) :=
  hf.isClosedMap.isEvenlyCovered_of_openPartialHomeomorph
    ((isClosed_singleton.preimage hf).isCompact.finite (.of_openPartialHomeomorph f subset_rfl h)) h

/-- If `f : E ‚Üí X` is a continuous map between Hausdorff spaces with `E` compact, `s` is a subset
of `X` such that `f` restricts to a homeomorphism on a neighborhood of every point of `f ‚Åª¬π' s`,
then `f` is a covering map on `s`.

For example, `s` can be taken to be the set of regular values of a C¬π map `f : E ‚Üí X`
where `E` and `X` are manifolds of the same dimension with `E` compact, according to
the inverse function theorem (see `ContDiffAt.toOpenPartialHomeomorph`). -/
theorem IsCoveringMapOn.of_openPartialHomeomorph
    [T2Space E] [T2Space X] [CompactSpace E] (hf : Continuous f)
    (h : ‚àÄ e ‚àà f ‚Åª¬π' s, ‚àÉ œÜ : OpenPartialHomeomorph E X, e ‚àà œÜ.source ‚àß œÜ = f) :
    IsCoveringMapOn f s :=
  fun x hx ‚Ü¶ .of_openPartialHomeomorph hf fun e he ‚Ü¶ h e (by apply he ‚ñ∏ hx)
