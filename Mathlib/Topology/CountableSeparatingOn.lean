/-
Copyright (c) 2023 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Topology.Separation
import Mathlib.Order.Filter.CountableSeparatingOn

/-!
# Countable separating families of sets in topological spaces

In this file we show that a T‚ÇÄ topological space with second countable
topology has a countable family of open (or closed) sets separating the points.
-/

set_option autoImplicit true

open Set TopologicalSpace

/-- If `X` is a topological space, `s` is a set in `X` such that the induced topology is T‚ÇÄ and is
second countable, then there exists a countable family of open sets in `X` that separates points
of `s`. -/
instance [TopologicalSpace X] {s : Set X} [T0Space s] [SecondCountableTopology s] :
    HasCountableSeparatingOn X IsOpen s := by
  suffices HasCountableSeparatingOn s IsOpen univ from .of_subtype fun _ ‚Ü¶ isOpen_induced_iff.1
  -- ‚ä¢ HasCountableSeparatingOn (‚Üës) IsOpen univ
  refine ‚ü®‚ü®countableBasis s, countable_countableBasis _, fun _ ‚Ü¶ isOpen_of_mem_countableBasis,
    fun x _ y _ h ‚Ü¶ ?_‚ü©‚ü©
  exact ((isBasis_countableBasis _).inseparable_iff.2 h).eq
  -- üéâ no goals

/-- If there exists a countable family of open sets separating points of `s`, then there exists
a countable family of closed sets separating points of `s`. -/
instance [TopologicalSpace X] {s : Set X} [h : HasCountableSeparatingOn X IsOpen s] :
    HasCountableSeparatingOn X IsClosed s :=
  let ‚ü®S, hSc, hSo, hS‚ü© := h.1
  ‚ü®compl '' S, hSc.image _, ball_image_iff.2 fun U hU ‚Ü¶ (hSo U hU).isClosed_compl,
    fun x hx y hy h ‚Ü¶ hS x hx y hy fun _U hU ‚Ü¶ not_iff_not.1 <| h _ (mem_image_of_mem _ hU)‚ü©
