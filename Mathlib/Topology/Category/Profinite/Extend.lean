/-
Copyright (c) 2024 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson
-/
import Mathlib.Topology.Category.Profinite.AsLimit
import Mathlib.Topology.Category.Profinite.CofilteredLimit
import Mathlib.CategoryTheory.Filtered.Final
/-!

# Extending cones in `Profinite`

Let `(Sáµ¢)_{i : I}` be a family of finite sets indexed by a cofiltered category `I` and let `S`Â be
its limit in `Profinite`. Let `G`Â be a functor from `Profinite`Â to a category `C`Â and suppose that
`G`Â preserves the limit described above. Suppose further that the projection maps `S âŸ¶ Sáµ¢` are
epimorphic for all `i`. Then `G.obj S` is isomorphic to a limit indexed by
`StructuredArrow S toProfinite` (see `Profinite.Extend.isLimitCone`).

We also provide the dual result for a functor of the form `G : Profiniteáµ’áµ– â¥¤ C`.

We apply this to define `Profinite.diagram'`, `Profinite.asLimitCone'`, and `Profinite.asLimit'`,
analogues to their unprimed versions in `Mathlib.Topology.Category.Profinite.AsLimit`, in which the
indexing category is `StructuredArrow S toProfinite`Â instead of `DiscreteQuotient S`.
-/

universe u w

open CategoryTheory Limits FintypeCat Functor

attribute [local instance] ConcreteCategory.instFunLike

namespace Profinite

variable {I : Type u} [SmallCategory I] [IsCofiltered I]
    {F : I â¥¤ FintypeCat.{max u w}} (c : Cone <| F â‹™ toProfinite)

/--
A continuous map from a profinite set to a finite set factors through one of the components of
the profinite set when written as a cofiltered limit of finite sets.
-/
lemma exists_hom (hc : IsLimit c) {X : FintypeCat} (f : c.pt âŸ¶ toProfinite.obj X) :
    âˆƒ (i : I) (g : F.obj i âŸ¶ X), f = c.Ï€.app i â‰« toProfinite.map g := by
  let _ : TopologicalSpace X := âŠ¥
  have : DiscreteTopology (toProfinite.obj X) := âŸ¨rflâŸ©
  let f' : LocallyConstant c.pt (toProfinite.obj X) :=
    âŸ¨f, (IsLocallyConstant.iff_continuous _).mpr f.continuousâŸ©
  obtain âŸ¨i, g, hâŸ© := exists_locallyConstant.{_, u} c hc f'
  refine âŸ¨i, (g : _ â†’ _), ?_âŸ©
  ext x
  exact LocallyConstant.congr_fun h x

namespace Extend

/--
Given a cone in `Profinite`,Â consisting of finite sets and indexed by a cofiltered category,
we obtain a functor from the indexing category to `StructuredArrow c.pt toProfinite`.
-/
@[simps]
def functor : I â¥¤ StructuredArrow c.pt toProfinite where
  obj i := StructuredArrow.mk (c.Ï€.app i)
  map f := StructuredArrow.homMk (F.map f) (c.w f)

-- We check that the original diagram factors through `Profinite.Extend.functor`.
example : functor c â‹™ StructuredArrow.proj c.pt toProfinite â‰… F := Iso.refl _

/--
Given a cone in `Profinite`,Â consisting of finite sets and indexed by a cofiltered category,
we obtain a functor from the opposite of the indexing category to
`CostructuredArrow toProfinite.op âŸ¨c.ptâŸ©`.
-/
@[simps! obj map]
def functorOp : Iáµ’áµ– â¥¤ CostructuredArrow toProfinite.op âŸ¨c.ptâŸ© :=
  (functor c).op â‹™ StructuredArrow.toCostructuredArrow _ _

-- We check that the opposite of the original diagram factors through `Profinite.Extend.functorOp`.
example : functorOp c â‹™ CostructuredArrow.proj toProfinite.op âŸ¨c.ptâŸ© â‰… F.op := Iso.refl _

/--
If the projection maps in the cone are epimorphic and the cone is limiting, then
`Profinite.Extend.functor` is initial.

TODO: investigate how to weaken the assumption `âˆ€ i, Epi (c.Ï€.app i)` to
`âˆ€ i, âˆƒ j (_ : j âŸ¶ i), Epi (c.Ï€.app j)`.
-/
lemma functor_initial (hc : IsLimit c) [âˆ€ i, Epi (c.Ï€.app i)] : Initial (functor c) := by
  let e : I â‰Œ ULiftHom.{w} (ULift.{w} I) := ULiftHomULiftCategory.equiv _
  suffices (e.inverse â‹™ functor c).Initial from initial_of_equivalence_comp e.inverse (functor c)
  rw [initial_iff_of_isCofiltered (F := e.inverse â‹™ functor c)]
  constructor
  Â· intro âŸ¨_, X, (f : c.pt âŸ¶ _)âŸ©
    obtain âŸ¨i, g, hâŸ© := exists_hom c hc f
    refine âŸ¨âŸ¨iâŸ©, âŸ¨StructuredArrow.homMk g h.symmâŸ©âŸ©
  Â· intro âŸ¨_, X, (f : c.pt âŸ¶ _)âŸ© âŸ¨iâŸ© âŸ¨_, (s : F.obj i âŸ¶ X), (w : f = c.Ï€.app i â‰« _)âŸ©
      âŸ¨_, (s' : F.obj i âŸ¶ X), (w' : f = c.Ï€.app i â‰« _)âŸ©
    simp only [functor_obj, functor_map, StructuredArrow.hom_eq_iff, StructuredArrow.mk_right,
      StructuredArrow.comp_right, StructuredArrow.homMk_right]
    refine âŸ¨âŸ¨iâŸ©, ðŸ™ _, ?_âŸ©
    simp only [CategoryTheory.Functor.map_id, Category.id_comp]
    rw [w] at w'
    exact toProfinite.map_injective <| Epi.left_cancellation _ _ w'

/--
If the projection maps in the cone are epimorphic and the cone is limiting, then
`Profinite.Extend.functorOp` is final.
-/
lemma functorOp_final (hc : IsLimit c) [âˆ€ i, Epi (c.Ï€.app i)] : Final (functorOp c) := by
  have := functor_initial c hc
  have : ((StructuredArrow.toCostructuredArrow toProfinite c.pt)).IsEquivalence  :=
    (inferInstance : (structuredArrowOpEquivalence _ _).functor.IsEquivalence )
  exact Functor.final_comp (functor c).op _

section Limit

variable {C : Type*} [Category C] (G : Profinite â¥¤ C)

/--
Given a functor `G` from `Profinite` and `S : Profinite`, we obtain a cone on
`(StructuredArrow.proj S toProfinite â‹™ toProfinite â‹™ G)` with cone point `G.obj S`.

Whiskering this cone with `Profinite.Extend.functor c` gives `G.mapCone c` as we check in the
example below.
-/
@[simps]
def cone (S : Profinite) :
    Cone (StructuredArrow.proj S toProfinite â‹™ toProfinite â‹™ G) where
  pt := G.obj S
  Ï€ := {
    app := fun i â†¦ G.map i.hom
    naturality := fun _ _ f â†¦ (by
      have := f.w
      simp only [const_obj_obj, StructuredArrow.left_eq_id, const_obj_map, Category.id_comp,
        StructuredArrow.w] at this
      simp only [const_obj_obj, comp_obj, StructuredArrow.proj_obj, const_obj_map, Category.id_comp,
        Functor.comp_map, StructuredArrow.proj_map, â† map_comp, StructuredArrow.w]) }

example : G.mapCone c = (cone G c.pt).whisker (functor c) := rfl

/--
If `c` and `G.mapCone c`Â are limit cones and the projection maps in `c` are epimorphic,
then `cone G c.pt` is a limit cone.
-/
noncomputable
def isLimitCone (hc : IsLimit c) [âˆ€ i, Epi (c.Ï€.app i)] (hc' : IsLimit <| G.mapCone c) :
    IsLimit (cone G c.pt) := (functor_initial c hc).isLimitWhiskerEquiv _ hc'

end Limit

section Colimit

variable {C : Type*} [Category C] (G : Profiniteáµ’áµ– â¥¤ C)

/--
Given a functor `G` from `Profiniteáµ’áµ–` and `S : Profinite`, we obtain a cocone on
`(CostructuredArrow.proj toProfinite.op âŸ¨SâŸ© â‹™ toProfinite.op â‹™ G)` with cocone point `G.obj âŸ¨SâŸ©`.

Whiskering this cocone with `Profinite.Extend.functorOp c` gives `G.mapCocone c.op` as we check in
the example below.
-/
@[simps]
def cocone (S : Profinite) :
    Cocone (CostructuredArrow.proj toProfinite.op âŸ¨SâŸ© â‹™ toProfinite.op â‹™ G) where
  pt := G.obj âŸ¨SâŸ©
  Î¹ := {
    app := fun i â†¦ G.map i.hom
    naturality := fun _ _ f â†¦ (by
      have := f.w
      simp only [op_obj, const_obj_obj, op_map, CostructuredArrow.right_eq_id, const_obj_map,
        Category.comp_id] at this
      simp only [comp_obj, CostructuredArrow.proj_obj, op_obj, const_obj_obj, Functor.comp_map,
        CostructuredArrow.proj_map, op_map, â† map_comp, this, const_obj_map, Category.comp_id]) }

example : G.mapCocone c.op = (cocone G c.pt).whisker (functorOp c) := rfl

/--
If `c` is a limit cone, `G.mapCocone c.op` is a colimit cone and the projection maps in `c`
are epimorphic, then `cocone G c.pt` is a colimit cone.
-/
noncomputable
def isColimitCocone (hc : IsLimit c) [âˆ€ i, Epi (c.Ï€.app i)] (hc' : IsColimit <| G.mapCocone c.op) :
    IsColimit (cocone G c.pt) := (functorOp_final c hc).isColimitWhiskerEquiv _ hc'

end Colimit

end Extend

open Extend

section ProfiniteAsLimit

variable (S : Profinite.{u})

/--
A functor `StructuredArrow S toProfinite â¥¤ FintypeCat`Â whose limit in `Profinite` is isomorphic
to `S`.
-/
abbrev fintypeDiagram' : StructuredArrow S toProfinite â¥¤ FintypeCat :=
  StructuredArrow.proj S toProfinite

/-- An abbreviation for `S.fintypeDiagram' â‹™ toProfinite`. -/
abbrev diagram' : StructuredArrow S toProfinite â¥¤ Profinite :=
  S.fintypeDiagram' â‹™ toProfinite

/-- A cone over `S.diagram'` whose cone point is `S`. -/
abbrev asLimitCone' : Cone (S.diagram') := cone (ðŸ­ _) S

instance (i : DiscreteQuotient S) : Epi (S.asLimitCone.Ï€.app i) :=
  (epi_iff_surjective _).mpr i.proj_surjective

/-- `S.asLimitCone'` is a limit cone. -/
noncomputable def asLimit' : IsLimit S.asLimitCone' := isLimitCone _ (ðŸ­ _) S.asLimit S.asLimit

/-- A bundled version of `S.asLimitCone'` and `S.asLimit'`. -/
noncomputable def lim' : LimitCone S.diagram' := âŸ¨S.asLimitCone', S.asLimit'âŸ©

end ProfiniteAsLimit

end Profinite
