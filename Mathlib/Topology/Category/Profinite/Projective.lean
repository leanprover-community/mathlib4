/-
Copyright (c) 2021 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin
-/
import Mathlib.Topology.Category.Profinite.Basic
import Mathlib.Topology.Compactification.StoneCech
import Mathlib.CategoryTheory.Preadditive.Projective.Basic
import Mathlib.CategoryTheory.ConcreteCategory.EpiMono

/-!
# Profinite sets have enough projectives

In this file we show that `Profinite` has enough projectives.

## Main results

Let `X` be a profinite set.

* `Profinite.projective_ultrafilter`: the space `Ultrafilter X` is a projective object
* `Profinite.projectivePresentation`: the natural map `Ultrafilter X â†’ X`
  is a projective presentation

-/


noncomputable section

universe u v w

open CategoryTheory Function

namespace Profinite

instance projective_ultrafilter (X : Type u) : Projective (of <| Ultrafilter X) where
  factors {Y Z} f g hg := by
    rw [epi_iff_surjective] at hg
    obtain âŸ¨g', hg'âŸ© := hg.hasRightInverse
    let t : X â†’ Y := g' âˆ˜ f âˆ˜ (pure : X â†’ Ultrafilter X)
    let h : Ultrafilter X â†’ Y := Ultrafilter.extend t
    have hh : Continuous h := continuous_ultrafilter_extend _
    use CompHausLike.ofHom _ âŸ¨h, hhâŸ©
    apply ConcreteCategory.coe_ext
    simp only [h]
    convert denseRange_pure.equalizer (g.hom.continuous.comp hh) f.hom.continuous _
    have : g.hom âˆ˜ g' = id := hg'.comp_eq_id
    rw [comp_assoc, ultrafilter_extend_extends, â† comp_assoc, this, id_comp]
    rfl

/-- For any profinite `X`, the natural map `Ultrafilter X â†’ X` is a projective presentation. -/
def projectivePresentation (X : Profinite.{u}) : ProjectivePresentation X where
  p := of <| Ultrafilter X
  f := CompHausLike.ofHom _ âŸ¨_, continuous_ultrafilter_extend idâŸ©
  projective := Profinite.projective_ultrafilter X
  epi := ConcreteCategory.epi_of_surjective _ fun x â†¦
    âŸ¨(pure x : Ultrafilter X), congr_fun (ultrafilter_extend_extends (ðŸ™ X)) xâŸ©

instance : EnoughProjectives Profinite.{u} where presentation X := âŸ¨projectivePresentation XâŸ©

end Profinite
