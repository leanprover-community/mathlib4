/-
Copyright (c) 2021 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin
-/
import Mathlib.Topology.Category.Profinite.Basic
import Mathlib.Topology.StoneCech
import Mathlib.CategoryTheory.Preadditive.Projective

#align_import topology.category.Profinite.projective from "leanprover-community/mathlib"@"829895f162a1f29d0133f4b3538f4cd1fb5bffd3"

/-!
# Profinite sets have enough projectives

In this file we show that `Profinite` has enough projectives.

## Main results

Let `X` be a profinite set.

* `Profinite.projective_ultrafilter`: the space `Ultrafilter X` is a projective object
* `Profinite.projectivePresentation`: the natural map `Ultrafilter X ‚Üí X`
  is a projective presentation

-/


noncomputable section

universe u v w

open CategoryTheory Function

-- This was a global instance prior to #13170. We may experiment with removing it.
attribute [local instance] ConcreteCategory.instFunLike

namespace Profinite

set_option linter.uppercaseLean3 false

instance projective_ultrafilter (X : Type u) : Projective (of <| Ultrafilter X) where
  factors {Y Z} f g hg := by
    rw [epi_iff_surjective] at hg
    obtain ‚ü®g', hg'‚ü© := hg.hasRightInverse
    let t : X ‚Üí Y := g' ‚àò f ‚àò (pure : X ‚Üí Ultrafilter X)
    let h : Ultrafilter X ‚Üí Y := Ultrafilter.extend t
    have hh : Continuous h := continuous_ultrafilter_extend _
    use ‚ü®h, hh‚ü©
    apply (forget Profinite).map_injective
    simp only [h, ContinuousMap.coe_mk, coe_comp]
    convert denseRange_pure.equalizer (g.continuous.comp hh) f.continuous _
     -- Porting note: same fix as in `Topology.Category.CompHaus.Projective`
    let g'' : ContinuousMap Y Z := g
    have : g'' ‚àò g' = id := hg'.comp_eq_id
    -- This used to be `rw`, but we need `erw` after leanprover/lean4#2644
    erw [comp.assoc, ultrafilter_extend_extends, ‚Üê comp.assoc, this, id_comp]
#align Profinite.projective_ultrafilter Profinite.projective_ultrafilter

/-- For any profinite `X`, the natural map `Ultrafilter X ‚Üí X` is a projective presentation. -/
def projectivePresentation (X : Profinite.{u}) : ProjectivePresentation X where
  p := of <| Ultrafilter X
  f := ‚ü®_, continuous_ultrafilter_extend id‚ü©
  projective := Profinite.projective_ultrafilter X
  epi := ConcreteCategory.epi_of_surjective _ fun x =>
    ‚ü®(pure x : Ultrafilter X), congr_fun (ultrafilter_extend_extends (ùüô X)) x‚ü©
#align Profinite.projective_presentation Profinite.projectivePresentation

instance : EnoughProjectives Profinite.{u} where presentation X := ‚ü®projectivePresentation X‚ü©

end Profinite
