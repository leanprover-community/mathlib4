/-
Copyright (c) 2023 Jon Eugster. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson, Boris Bolvig KjÃ¦r, Jon Eugster, Sina Hazratpour
-/
import Mathlib.CategoryTheory.Sites.Coherent
import Mathlib.Topology.Category.Profinite.Limits

/-!
# Effective epimorphic families in `Profinite`

Let `Ï€ a : X a âŸ¶ B` be a family of morphisms in `Profinite` indexed by a finite type `Î±`.
In this file, we show that the following are all equivalent:
- The family `Ï€` is effective epimorphic.
- The induced map `âˆ X âŸ¶ B` is epimorphic.
- The family `Ï€` is jointly surjective.

## Main results

- `Profinite.effectiveEpiFamily_tfae`: characterise being an effective epimorphic family.
- `Profinite.instPrecoherent`: `Profinite` is precoherent.

## Implementation notes

The entire section `EffectiveEpiFamily` comprises exclusively a technical construction for
the main proof and does not contain any statements that would be useful in other contexts.
-/

open CategoryTheory Limits

namespace Profinite

/-!
This section contains exclusively technical definitions and results that are used
in the proof of `Profinite.effectiveEpiFamily_of_jointly_surjective`.

The construction of `QB` as a quotient of the maps `X a â†’ B` is analoguous to the
construction in the file `CompHaus.EffectiveEpi`,
but one has to start with an equivalence relation on `Profinite` instead.
-/
namespace EffectiveEpiFamily

/- Assume we have a family `X a â†’ B` which is jointly surjective. -/
variable {Î± : Type} [Fintype Î±] {B : Profinite}
  {X : Î± â†’ Profinite} (Ï€ : (a : Î±) â†’ (X a âŸ¶ B))
  (surj : âˆ€ b : B, âˆƒ (a : Î±) (x : X a), Ï€ a x = b)

/--
Implementation: This is a setoid on the explicit finite coproduct of `X` whose quotient
will be isomorphic to `B` provided that `X a â†’ B` is an effective epi family.
-/
def relation : Setoid (finiteCoproduct X) where
  r a b := âˆƒ (Z : Profinite) (z : Z) (fst : Z âŸ¶ X a.fst) (snd : Z âŸ¶ X b.fst),
    fst â‰« Ï€ _ = snd â‰« Ï€ _ âˆ§ fst z = a.snd âˆ§ snd z = b.snd
  iseqv := by
    constructor
    Â· rintro âŸ¨a, xâŸ©
      exact âŸ¨X a, x, ğŸ™ _, ğŸ™ _, by simp, rfl, rflâŸ©
    Â· rintro âŸ¨a, xâŸ© âŸ¨b, yâŸ© âŸ¨Z, z, fst, snd, w, h1, h2âŸ©
      exact âŸ¨Z, z, snd, fst, w.symm, h2, h1âŸ©
    Â· rintro âŸ¨a, xâŸ© âŸ¨b, yâŸ© âŸ¨z, câŸ© âŸ¨Z, z, fstZ, sndZ, hZ, hZ1, hZ2âŸ©
      rintro âŸ¨W, w, fstW, sndW, hW, hW1, hW2âŸ©
      refine âŸ¨pullback sndZ fstW, âŸ¨âŸ¨z, wâŸ©, by dsimp; rw [hZ2, hW1]âŸ©,
        pullback.fst _ _ â‰« fstZ, pullback.snd _ _ â‰« sndW, ?_, hZ1, hW2âŸ©
      dsimp only at *
      simp only [Category.assoc, hZ, â† hW]
      apply ContinuousMap.ext
      rintro âŸ¨âŸ¨u, vâŸ©, hâŸ©
      change Ï€ b (sndZ u) = Ï€ b (fstW v)
      rw [h]

/--
Implementation: The map from the quotient of `relation Ï€` to `B`, which will eventually
become the function underlying an isomorphism, provided that `X a â†’ B` is an effective epi family.
-/
def Î¹Fun : Quotient (relation Ï€) â†’ B :=
  Quotient.lift (fun âŸ¨a, xâŸ© => Ï€ a x) <| by
    rintro âŸ¨a, xâŸ© âŸ¨b, yâŸ© âŸ¨Z, z, fst, snd, h, hx, hyâŸ©
    dsimp only at *
    rw [â† hx, â† hy]
    apply_fun (Â· z) at h
    exact h

/-- Implementation: `Î¹Fun` is continous. -/
lemma Î¹Fun_continuous : Continuous (Î¹Fun Ï€) := by
  apply Continuous.quotient_lift
  apply continuous_sigma
  intro a
  exact (Ï€ a).continuous

/-- Implementation: `Î¹Fun` is injective. -/
lemma Î¹Fun_injective : (Î¹Fun Ï€).Injective := by
  rintro âŸ¨âŸ¨a, xâŸ©âŸ© âŸ¨âŸ¨b, yâŸ©âŸ© (h : Ï€ _ _ = Ï€ _ _)
  apply Quotient.sound'
  refine âŸ¨pullback (Ï€ a) (Ï€ b), âŸ¨âŸ¨x, yâŸ©, hâŸ©, pullback.fst _ _, pullback.snd _ _, ?_, rfl, rflâŸ©
  ext âŸ¨_, hâŸ©
  exact h

/-- Implementation: The quotient of `relation Ï€`, considered as an object of `CompHaus`. -/
def QB' : CompHaus :=
  haveI : T2Space (Quotient <| relation Ï€) :=
    âŸ¨fun _ _ h => separated_by_continuous (Î¹Fun_continuous Ï€) <| (Î¹Fun_injective Ï€).ne h âŸ©
  CompHaus.of (Quotient <| relation Ï€)

/-- Implementation: The function `Î¹Fun`, considered as a morphism in `CompHaus`. -/
def Î¹Hom' : (QB' Ï€) âŸ¶ B.toCompHaus := âŸ¨Î¹Fun Ï€, Î¹Fun_continuous Ï€âŸ©

/-- Implementation: `Î¹Fun` as isomorphism in `CompHaus`. -/
noncomputable
def Î¹Iso' : (QB' Ï€) â‰… B.toCompHaus :=
  have : IsIso (Î¹Hom' Ï€) := by
    apply CompHaus.isIso_of_bijective
    refine âŸ¨Î¹Fun_injective _, ?_âŸ©
    intro b
    obtain âŸ¨a, x, hâŸ© := surj b
    exact âŸ¨Quotient.mk _ âŸ¨a, xâŸ©, hâŸ©
  asIso (Î¹Hom' Ï€)

/-- Implementation: The quotient of `relation Ï€`, considered as an object of `Profinite`. -/
def QB : Profinite where
  toCompHaus := QB' Ï€
  IsTotallyDisconnected := âŸ¨(CompHaus.homeoOfIso (Î¹Iso' Ï€ surj)).embedding.isTotallyDisconnected
    (isTotallyDisconnected_of_totallyDisconnectedSpace _)âŸ©

/-- Implementation: The function `Î¹Fun`, considered as a morphism in `Profinite`. -/
def Î¹Hom : (QB Ï€ surj) âŸ¶ B := âŸ¨Î¹Fun Ï€, Î¹Fun_continuous Ï€âŸ©

/-- Implementation: `Î¹Fun` as an isomorphism in `Profinite`. -/
noncomputable
def Î¹Iso : (QB Ï€ surj) â‰… B :=
  have : IsIso (Î¹Hom Ï€ surj) := by
    apply Profinite.isIso_of_bijective
    refine âŸ¨Î¹Fun_injective _, ?_âŸ©
    intro b
    obtain âŸ¨a, x, hâŸ© := surj b
    exact âŸ¨Quotient.mk _ âŸ¨a, xâŸ©, hâŸ©
  asIso (Î¹Hom Ï€ surj)

/-- Implementation: The family of morphisms `X a âŸ¶ QB` which will be shown to be effective epi. -/
def Ï€' : (a : Î±) â†’ (X a âŸ¶ QB Ï€ surj) := fun a =>
  { toFun := fun x => Quotient.mk _ âŸ¨a, xâŸ©
    continuous_toFun := by
      apply Continuous.comp
      apply continuous_quot_mk
      apply continuous_sigmaMk (Ïƒ := (X Â·)) }

/-- Implementation: The family of morphisms `Ï€' a : X a âŸ¶ QB` is an effective epi. -/
def structAux : EffectiveEpiFamilyStruct X (Ï€' Ï€ surj) where
  desc := fun e h => {
    toFun := Quotient.lift (fun âŸ¨a, xâŸ© => e a x) <| by
      rintro âŸ¨a, xâŸ© âŸ¨b, yâŸ© âŸ¨Z, z, fst, snd, hh, hx, hyâŸ©
      dsimp at *
      rw [â† hx, â† hy]
      specialize h _ _ fst snd ?_
      Â· ext z
        apply Î¹Fun_injective
        apply_fun (Â· z) at hh
        exact hh
      apply_fun (Â· z) at h
      exact h
    continuous_toFun := by
      apply Continuous.quotient_lift
      apply continuous_sigma
      intro a
      exact (e a).continuous }
  fac := by
    intro Z e h a
    ext
    rfl
  uniq := by
    intro Z e h m hm
    ext âŸ¨âŸ¨a, xâŸ©âŸ©
    specialize hm a
    apply_fun (Â· x) at hm
    exact hm

/-- Implementation: `Î¹Iso âˆ˜ (Ï€' a) : X a â†’ QB â†’ B` is exactly `Ï€ a`. -/
@[reassoc]
lemma Ï€'_comp_Î¹_hom (a : Î±) : Ï€' Ï€ surj a â‰« (Î¹Iso Ï€ surj).hom = Ï€ a := by
  ext
  rfl

/-- Implementation: `Î¹Isoâ»Â¹ âˆ˜ (Ï€ a) : X a â†’ B â†’ QB` is exactly `Ï€' a`. -/
@[reassoc]
lemma Ï€_comp_Î¹_inv (a : Î±) : Ï€ a â‰« (Î¹Iso Ï€ surj).inv = Ï€' Ï€ surj a := by
  rw [Iso.comp_inv_eq]
  exact Ï€'_comp_Î¹_hom _ surj _

/--
Implementation: The family `X` is an effective epi, provided that `Ï€` are jointly surjective.
The theorem `Profinite.effectiveEpiFamily_tfae` should be used instead.
-/
noncomputable
def struct : EffectiveEpiFamilyStruct X Ï€ where
  desc := fun e h => (Î¹Iso Ï€ surj).inv â‰« (structAux Ï€ surj).desc e (fun aâ‚ aâ‚‚ gâ‚ gâ‚‚ hh => by
    apply h
    rw [â† cancel_mono (Î¹Iso _ surj).inv]
    simpa only [Category.assoc, Ï€_comp_Î¹_inv])
  fac := by
    intro W e h a
    simp only [Eq.ndrec, id_eq, eq_mpr_eq_cast, Ï€_comp_Î¹_inv_assoc, (structAux Ï€ surj).fac]
  uniq := by
    intro W e h m hm
    dsimp
    rw [Iso.eq_inv_comp]
    apply (structAux Ï€ surj).uniq
    intro a
    simpa using hm a

end EffectiveEpiFamily

section JointlySurjective

/-- One direction of `Profinite.effectiveEpiFamily_tfae` -/
theorem effectiveEpiFamily_of_jointly_surjective
    {Î± : Type} [Fintype Î±] {B : Profinite}
    (X : Î± â†’ Profinite) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B))
    (surj : âˆ€ b : B, âˆƒ (a : Î±) (x : X a), Ï€ a x = b) :
    EffectiveEpiFamily X Ï€ :=
  âŸ¨âŸ¨Profinite.EffectiveEpiFamily.struct Ï€ surjâŸ©âŸ©

open List in
/--
For a finite family of profinite spaces `Ï€ a : X a â†’ B` the following are equivalent:
* `Ï€` is an effective epimorphic family
* the map `âˆ Ï€ a âŸ¶ B` is an epimorphism
* `Ï€` is jointly surjective
-/
theorem effectiveEpiFamily_tfae {Î± : Type} [Fintype Î±] {B : Profinite}
    (X : Î± â†’ Profinite) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B)) :
    TFAE [
      EffectiveEpiFamily X Ï€,
      Epi (Limits.Sigma.desc Ï€),
      âˆ€ (b : B), âˆƒ (a : Î±) (x : X a), Ï€ a x = b ] := by
  tfae_have 1 â†’ 2
  Â· intro
    infer_instance
  tfae_have 2 â†’ 3
  Â· intro e
    rw [epi_iff_surjective] at e
    let i : âˆ X â‰… finiteCoproduct X :=
      (colimit.isColimit _).coconePointUniqueUpToIso (finiteCoproduct.isColimit _)
    intro b
    obtain âŸ¨t, rflâŸ© := e b
    let q := i.hom t
    refine âŸ¨q.1, q.2, ?_âŸ©
    have : t = i.inv (i.hom t)
    Â· show t = (i.hom â‰« i.inv) t
      simp only [i.hom_inv_id]
      rfl
    rw [this]
    show _ = (i.inv â‰« Sigma.desc Ï€) (i.hom t)
    suffices : i.inv â‰« Sigma.desc Ï€ = finiteCoproduct.desc X Ï€
    Â· rw [this]
      rfl
    rw [Iso.inv_comp_eq]
    apply colimit.hom_ext
    rintro âŸ¨aâŸ©
    simp only [Discrete.functor_obj, colimit.Î¹_desc, Cofan.mk_pt, Cofan.mk_Î¹_app,
      colimit.comp_coconePointUniqueUpToIso_hom_assoc]
    ext
    rfl
  tfae_have 3 â†’ 1
  Â· apply effectiveEpiFamily_of_jointly_surjective
  tfae_finish

end JointlySurjective

section Coherent

/-- The category of profinite spaces is precoherent -/
instance instPrecoherent : Precoherent Profinite := by
  constructor
  intro Bâ‚ Bâ‚‚ f Î± _ Xâ‚ Ï€â‚ hâ‚
  refine âŸ¨Î±, inferInstance, (pullback f <| Ï€â‚ Â·), fun a => pullback.fst _ _, ?_,
    id, fun a => pullback.snd _ _, ?_âŸ©
  Â· have := (effectiveEpiFamily_tfae _ Ï€â‚).out 0 2
    rw [this] at hâ‚
    clear this
    have := (effectiveEpiFamily_tfae _ (fun a => pullback.fst f (Ï€â‚ a))).out 0 2
    rw [this]
    clear this
    intro bâ‚‚
    obtain âŸ¨a, x, hâŸ© := hâ‚ (f bâ‚‚)
    exact âŸ¨a, âŸ¨âŸ¨bâ‚‚, xâŸ©, h.symmâŸ©, rflâŸ©
  Â· intro a
    dsimp
    ext âŸ¨âŸ¨_, _âŸ©, hâŸ©
    exact h.symm

end Coherent

end Profinite
