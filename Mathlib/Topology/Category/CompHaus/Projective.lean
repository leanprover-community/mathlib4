/-
Copyright (c) 2021 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin
-/
import Mathlib.Topology.Category.CompHaus.Basic
import Mathlib.Topology.StoneCech
import Mathlib.CategoryTheory.Preadditive.Projective

#align_import topology.category.CompHaus.projective from "leanprover-community/mathlib"@"829895f162a1f29d0133f4b3538f4cd1fb5bffd3"

/-!
# CompHaus has enough projectives

In this file we show that `CompHaus` has enough projectives.

## Main results

Let `X` be a compact Hausdorff space.

* `CompHaus.projective_ultrafilter`: the space `Ultrafilter X` is a projective object
* `CompHaus.projective_presentation`: the natural map `Ultrafilter X ‚Üí X`
  is a projective presentation

## Reference

See [miraglia2006introduction] Chapter 21 for a proof that `CompHaus` has enough projectives.

-/


noncomputable section

open CategoryTheory Function

namespace CompHaus

attribute [local instance] ConcreteCategory.funLike

instance projective_ultrafilter (X : Type*) : Projective (of <| Ultrafilter X)
    where
  factors {Y Z} f g hg := by
    rw [epi_iff_surjective] at hg
    -- ‚ä¢ ‚àÉ f', f' ‚â´ g = f
    obtain ‚ü®g', hg'‚ü© := hg.hasRightInverse
    -- ‚ä¢ ‚àÉ f', f' ‚â´ g = f
    let t : X ‚Üí Y := g' ‚àò f ‚àò (pure : X ‚Üí Ultrafilter X)
    -- ‚ä¢ ‚àÉ f', f' ‚â´ g = f
    let h : Ultrafilter X ‚Üí Y := Ultrafilter.extend t
    -- ‚ä¢ ‚àÉ f', f' ‚â´ g = f
    have hh : Continuous h := continuous_ultrafilter_extend _
    -- ‚ä¢ ‚àÉ f', f' ‚â´ g = f
    use ‚ü®h, hh‚ü©
    -- ‚ä¢ ContinuousMap.mk h ‚â´ g = f
    apply Faithful.map_injective (F := forget CompHaus)
    -- ‚ä¢ (forget CompHaus).map (ContinuousMap.mk h ‚â´ g) = (forget CompHaus).map f
    simp only [Functor.map_comp, ContinuousMap.coe_mk, coe_comp]
    -- ‚ä¢ (forget CompHaus).map (ContinuousMap.mk (Ultrafilter.extend (g' ‚àò ‚Üëf ‚àò pure) ‚Ä¶
    convert denseRange_pure.equalizer (g.continuous.comp hh) f.continuous _
    -- ‚ä¢ (‚Üëg ‚àò h) ‚àò pure = ‚Üëf ‚àò pure
    -- Porting note: We need to get the coercions to functions under control.
    -- The next two lines should not be needed.
    let g'' : ContinuousMap Y Z := g
    -- ‚ä¢ (‚Üëg ‚àò h) ‚àò pure = ‚Üëf ‚àò pure
    have : g'' ‚àò g' = id := hg'.comp_eq_id
    -- ‚ä¢ (‚Üëg ‚àò h) ‚àò pure = ‚Üëf ‚àò pure
    rw [comp.assoc, ultrafilter_extend_extends, ‚Üê comp.assoc, this, comp.left_id]
    -- üéâ no goals
set_option linter.uppercaseLean3 false in
#align CompHaus.projective_ultrafilter CompHaus.projective_ultrafilter

/-- For any compact Hausdorff space `X`,
  the natural map `Ultrafilter X ‚Üí X` is a projective presentation. -/
def projectivePresentation (X : CompHaus) : ProjectivePresentation X where
  p := of <| Ultrafilter X
  f := ‚ü®_, continuous_ultrafilter_extend id‚ü©
  projective := CompHaus.projective_ultrafilter X
  epi :=
    ConcreteCategory.epi_of_surjective _ fun x =>
      ‚ü®(pure x : Ultrafilter X), congr_fun (ultrafilter_extend_extends (ùüô X)) x‚ü©
set_option linter.uppercaseLean3 false in
#align CompHaus.projective_presentation CompHaus.projectivePresentation

instance : EnoughProjectives CompHaus where presentation X := ‚ü®projectivePresentation X‚ü©

end CompHaus
