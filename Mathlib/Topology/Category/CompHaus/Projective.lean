/-
Copyright (c) 2021 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin
-/
import Mathlib.Topology.Category.CompHaus.Basic
import Mathlib.Topology.Compactification.StoneCech
import Mathlib.CategoryTheory.Preadditive.Projective.Basic
import Mathlib.CategoryTheory.ConcreteCategory.EpiMono

/-!
# CompHaus has enough projectives

In this file we show that `CompHaus` has enough projectives.

## Main results

Let `X` be a compact Hausdorff space.

* `CompHaus.projective_ultrafilter`: the space `Ultrafilter X` is a projective object
* `CompHaus.projectivePresentation`: the natural map `Ultrafilter X â†’ X`
  is a projective presentation

## Reference

See [miraglia2006introduction] Chapter 21 for a proof that `CompHaus` has enough projectives.

-/


noncomputable section

open CategoryTheory Function

namespace CompHaus

instance projective_ultrafilter (X : Type*) : Projective (of <| Ultrafilter X) where
  factors {Y Z} f g hg := by
    rw [epi_iff_surjective] at hg
    obtain âŸ¨g', hg'âŸ© := hg.hasRightInverse
    let t : X â†’ Y := g' âˆ˜ f âˆ˜ (pure : X â†’ Ultrafilter X)
    let h : Ultrafilter X â†’ Y := Ultrafilter.extend t
    have hh : Continuous h := continuous_ultrafilter_extend _
    use CompHausLike.ofHom _ âŸ¨h, hhâŸ©
    apply ConcreteCategory.coe_ext
    have : g.hom âˆ˜ g' = id := hg'.comp_eq_id
    convert denseRange_pure.equalizer (g.hom.continuous.comp hh) f.hom.continuous _
    -- This used to be `rw`, but we need `rw; rfl` after https://github.com/leanprover/lean4/pull/2644
    rw [comp_assoc, ultrafilter_extend_extends, â† comp_assoc, this, id_comp]
    rfl

/-- For any compact Hausdorff space `X`,
  the natural map `Ultrafilter X â†’ X` is a projective presentation. -/
def projectivePresentation (X : CompHaus) : ProjectivePresentation X where
  p := of <| Ultrafilter X
  f := CompHausLike.ofHom _ âŸ¨_, continuous_ultrafilter_extend idâŸ©
  projective := CompHaus.projective_ultrafilter X
  epi :=
    ConcreteCategory.epi_of_surjective _ fun x â†¦
      âŸ¨(pure x : Ultrafilter X), congr_fun (ultrafilter_extend_extends (ðŸ™ X)) xâŸ©

instance : EnoughProjectives CompHaus where presentation X := âŸ¨projectivePresentation XâŸ©

end CompHaus
