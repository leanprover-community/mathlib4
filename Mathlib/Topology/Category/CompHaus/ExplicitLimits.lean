/-
Copyright (c) 2023 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/

import Mathlib.Topology.Category.CompHaus.Basic
import Mathlib.CategoryTheory.Limits.Shapes.Pullbacks

/-!

# Explicit limits and colimits

This file collects some constructions of explicit limits and colimits in `CompHaus`,
which may be useful due to their definitional properties.

So far, we have the following:
- Explicit pullbacks, defined in the "usual" way as a subset of the product.
- Explicit finite coproducts, defined as a disjoint union.

-/

namespace CompHaus

universe u

open CategoryTheory

section Pullbacks

variable {X Y B : CompHaus.{u}} (f : X âŸ¶ B) (g : Y âŸ¶ B)

/--
The pullback of two morphisms `f,g` in `CompHaus`, constructed explicitly as the set of
pairs `(x,y)` such that `f x = g y`, with the topology induced by the product.
-/
def pullback : CompHaus.{u} :=
  letI set := { xy : X Ã— Y | f xy.fst = g xy.snd }
  haveI : CompactSpace set := by
    rw [â† isCompact_iff_compactSpace]
    apply IsClosed.isCompact
    exact isClosed_eq (f.continuous.comp continuous_fst) (g.continuous.comp continuous_snd)
  CompHaus.of set

/--
The projection from the pullback to the first component.
-/
def pullback.fst : pullback f g âŸ¶ X where
  toFun := fun âŸ¨âŸ¨x,_âŸ©,_âŸ© => x
  continuous_toFun := Continuous.comp continuous_fst continuous_subtype_val

/--
The projection from the pullback to the second component.
-/
def pullback.snd : pullback f g âŸ¶ Y where
  toFun := fun âŸ¨âŸ¨_,yâŸ©,_âŸ© => y
  continuous_toFun := Continuous.comp continuous_snd continuous_subtype_val

@[reassoc]
lemma pullback.condition : pullback.fst f g â‰« f = pullback.snd f g â‰« g := by
  ext âŸ¨_,hâŸ©; exact h

/--
Construct a morphism to the explicit pullback given morphisms to the factors
which are compatible with the maps to the base.
This is essentially the universal property of the pullback.
-/
def pullback.lift {Z : CompHaus.{u}} (a : Z âŸ¶ X) (b : Z âŸ¶ Y) (w : a â‰« f = b â‰« g) :
    Z âŸ¶ pullback f g where
  toFun := fun z => âŸ¨âŸ¨a z, b zâŸ©, by apply_fun (fun q => q z) at w; exact wâŸ©
  continuous_toFun := by
    apply Continuous.subtype_mk
    rw [continuous_prod_mk]
    exact âŸ¨a.continuous, b.continuousâŸ©

@[reassoc (attr := simp)]
lemma pullback.lift_fst {Z : CompHaus.{u}} (a : Z âŸ¶ X) (b : Z âŸ¶ Y) (w : a â‰« f = b â‰« g) :
  pullback.lift f g a b w â‰« pullback.fst f g = a := rfl

@[reassoc (attr := simp)]
lemma pullback.lift_snd {Z : CompHaus.{u}} (a : Z âŸ¶ X) (b : Z âŸ¶ Y) (w : a â‰« f = b â‰« g) :
  pullback.lift f g a b w â‰« pullback.snd f g = b := rfl

lemma pullback.hom_ext {Z : CompHaus.{u}} (a b : Z âŸ¶ pullback f g)
    (hfst : a â‰« pullback.fst f g = b â‰« pullback.fst f g)
    (hsnd : a â‰« pullback.snd f g = b â‰« pullback.snd f g) : a = b := by
  ext z
  apply_fun (fun q => q z) at hfst hsnd
  apply Subtype.ext
  apply Prod.ext
  Â· exact hfst
  Â· exact hsnd

/--
The pullback cone whose cone point is the explicit pullback.
-/
@[simps! pt Ï€]
def pullback.cone : Limits.PullbackCone f g :=
  Limits.PullbackCone.mk (pullback.fst f g) (pullback.snd f g) (pullback.condition f g)

/--
The explicit pullback cone is a limit cone.
-/
@[simps! lift]
def pullback.isLimit : Limits.IsLimit (pullback.cone f g) :=
  Limits.PullbackCone.isLimitAux _
    (fun s => pullback.lift f g s.fst s.snd s.condition)
    (fun _ => pullback.lift_fst _ _ _ _ _)
    (fun _ => pullback.lift_snd _ _ _ _ _)
    (fun _ _ hm => pullback.hom_ext _ _ _ _ (hm .left) (hm .right))

section Isos

noncomputable
def ToExplicit : Limits.pullback f g âŸ¶ CompHaus.pullback f g :=
  pullback.lift f g Limits.pullback.fst Limits.pullback.snd Limits.pullback.condition

noncomputable
def FromExplicit : CompHaus.pullback f g âŸ¶ Limits.pullback f g :=
  Limits.pullback.lift (pullback.fst _ _) (pullback.snd _ _) (pullback.condition f g)

@[simp]
theorem toExplicit_comp_fromExplicit_eq_id :
    (ToExplicit f g â‰« FromExplicit f g) = ğŸ™ _ := by
  refine' Limits.pullback.hom_ext (k := (ToExplicit f g â‰« FromExplicit f g)) _ _
  Â· simp [ToExplicit, FromExplicit]
  Â· rw [Category.id_comp, Category.assoc, FromExplicit, Limits.pullback.lift_snd,
      ToExplicit, pullback.lift_snd]

@[simp]
theorem fromExplicit_comp_toExplicit_eq_id :
    (FromExplicit f g â‰« ToExplicit f g) = ğŸ™ _ :=
  pullback.hom_ext f g _ _ (by simp [ToExplicit, FromExplicit]) (by simp [ToExplicit, FromExplicit])

@[simps]
noncomputable
def FromExplicitIso : CompHaus.pullback f g â‰… Limits.pullback f g where
  hom := FromExplicit f g
  inv := ToExplicit f g
  hom_inv_id := fromExplicit_comp_toExplicit_eq_id f g
  inv_hom_id := toExplicit_comp_fromExplicit_eq_id f g

end Isos

section Compatibility

theorem explicit_fst_eq :
    CompHaus.pullback.fst f g = FromExplicit f g â‰« Limits.pullback.fst := by
  dsimp [FromExplicit]
  simp only [Limits.limit.lift_Ï€, Limits.PullbackCone.mk_pt, Limits.PullbackCone.mk_Ï€_app]

theorem explicit_snd_eq :
    CompHaus.pullback.snd f i = FromExplicit f i â‰« Limits.pullback.snd := by
  dsimp [FromExplicit]
  simp only [Limits.limit.lift_Ï€, Limits.PullbackCone.mk_pt, Limits.PullbackCone.mk_Ï€_app]

end Compatibility

end Pullbacks

section FiniteCoproducts

variable {Î± : Type} [Fintype Î±] (X : Î± â†’ CompHaus.{u})

/--
The coproduct of a finite family of objects in `CompHaus`, constructed as the disjoint
union with its usual topology.
-/
def finiteCoproduct : CompHaus := CompHaus.of <| Î£ (a : Î±), X a

/--
The inclusion of one of the factors into the explicit finite coproduct.
-/
def finiteCoproduct.Î¹ (a : Î±) : X a âŸ¶ finiteCoproduct X where
  toFun := fun x => âŸ¨a,xâŸ©
  continuous_toFun := continuous_sigmaMk (Ïƒ := fun a => X a)

/--
To construct a morphism from the explicit finite coproduct, it suffices to
specify a morphism from each of its factors.
This is essentially the universal property of the coproduct.
-/
def finiteCoproduct.desc {B : CompHaus.{u}} (e : (a : Î±) â†’ (X a âŸ¶ B)) :
    finiteCoproduct X âŸ¶ B where
  toFun := fun âŸ¨a,xâŸ© => e a x
  continuous_toFun := by
    apply continuous_sigma
    intro a; exact (e a).continuous

@[reassoc (attr := simp)]
lemma finiteCoproduct.Î¹_desc {B : CompHaus.{u}} (e : (a : Î±) â†’ (X a âŸ¶ B)) (a : Î±) :
  finiteCoproduct.Î¹ X a â‰« finiteCoproduct.desc X e = e a := rfl

lemma finiteCoproduct.hom_ext {B : CompHaus.{u}} (f g : finiteCoproduct X âŸ¶ B)
    (h : âˆ€ a : Î±, finiteCoproduct.Î¹ X a â‰« f = finiteCoproduct.Î¹ X a â‰« g) : f = g := by
  ext âŸ¨a,xâŸ©
  specialize h a
  apply_fun (fun q => q x) at h
  exact h

/--
The coproduct cocone associated to the explicit finite coproduct.
-/
@[simps]
def finiteCoproduct.cocone : Limits.Cocone (Discrete.functor X) where
  pt := finiteCoproduct X
  Î¹ := Discrete.natTrans fun âŸ¨aâŸ© => finiteCoproduct.Î¹ X a

/--
The explicit finite coproduct cocone is a colimit cocone.
-/
@[simps]
def finiteCoproduct.isColimit : Limits.IsColimit (finiteCoproduct.cocone X) where
  desc := fun s => finiteCoproduct.desc _ fun a => s.Î¹.app âŸ¨aâŸ©
  fac := fun s âŸ¨aâŸ© => finiteCoproduct.Î¹_desc _ _ _
  uniq := fun s m hm => finiteCoproduct.hom_ext _ _ _ fun a => by
    specialize hm âŸ¨aâŸ©
    ext t
    apply_fun (fun q => q t) at hm
    exact hm

section Iso

noncomputable
def ToFiniteCoproduct : âˆ X âŸ¶ finiteCoproduct X :=
  Limits.Sigma.desc <| fun a => finiteCoproduct.Î¹ X a â‰« (ğŸ™ _)

noncomputable
def FromFiniteCoproduct : finiteCoproduct X âŸ¶ âˆ X :=
  finiteCoproduct.desc X <| fun a => (Limits.Sigma.Î¹ X a)

@[simp]
theorem toFiniteCoproduct_comp_fromFiniteCoproduct_eq_id :
    (ToFiniteCoproduct X â‰« FromFiniteCoproduct X) = ğŸ™ _ := by
  ext
  simp [ToFiniteCoproduct, FromFiniteCoproduct]

@[simp]
theorem fromFiniteCoproduct_comp_toFiniteCoproduct_eq_id :
    (FromFiniteCoproduct X â‰« ToFiniteCoproduct X) = ğŸ™ _ := by
  refine' finiteCoproduct.hom_ext _ _ _ (fun a => _)
  simp [ToFiniteCoproduct, FromFiniteCoproduct]

@[simps]
noncomputable
def FromFiniteCoproductIso : finiteCoproduct X â‰… âˆ X where
  hom := FromFiniteCoproduct X
  inv := ToFiniteCoproduct X
  hom_inv_id := fromFiniteCoproduct_comp_toFiniteCoproduct_eq_id X
  inv_hom_id := toFiniteCoproduct_comp_fromFiniteCoproduct_eq_id X

@[simps]
noncomputable
def ToFiniteCoproductIso : âˆ X â‰… finiteCoproduct X where
  hom := ToFiniteCoproduct X
  inv := FromFiniteCoproduct X
  hom_inv_id := toFiniteCoproduct_comp_fromFiniteCoproduct_eq_id X
  inv_hom_id := fromFiniteCoproduct_comp_toFiniteCoproduct_eq_id X

theorem isIso_toFiniteCoproduct :
    IsIso (ToFiniteCoproduct X) :=
  âŸ¨FromFiniteCoproduct X, by simp, by simpâŸ©

theorem isIso_fromFiniteCoproduct :
    IsIso (FromFiniteCoproduct X) :=
  âŸ¨ToFiniteCoproduct X, by simp, by simpâŸ©

@[simp]
theorem Sigma.Î¹_comp_toFiniteCoproduct (a : Î±) :
    (Limits.Sigma.Î¹ X a) â‰« ToFiniteCoproduct X = finiteCoproduct.Î¹ X a := by
  simp [ToFiniteCoproduct]

@[simp]
theorem finiteCoproduct.Î¹_comp_fromFiniteCoproduct (a : Î±) :
    (finiteCoproduct.Î¹ X a) â‰« FromFiniteCoproduct X = Limits.Sigma.Î¹ X a := by
  simp [FromFiniteCoproduct]

@[simps] noncomputable
def ToFiniteCoproductHomeo : (âˆ X : _) â‰ƒâ‚œ finiteCoproduct X where
  toFun := ToFiniteCoproduct X
  invFun := FromFiniteCoproduct X
  left_inv := fun x => by
    change (ToFiniteCoproduct X â‰« FromFiniteCoproduct X) x = x
    simp only [toFiniteCoproduct_comp_fromFiniteCoproduct_eq_id, id_apply]
  right_inv := fun x => by
    change (FromFiniteCoproduct X â‰« ToFiniteCoproduct X) x = x
    simp only [fromFiniteCoproduct_comp_toFiniteCoproduct_eq_id, id_apply]
  continuous_toFun := (ToFiniteCoproduct X).continuous_toFun
  continuous_invFun := (FromFiniteCoproduct X).continuous_toFun

theorem ToFiniteCoproduct.OpenEmbedding : OpenEmbedding (ToFiniteCoproductHomeo X) :=
  Homeomorph.openEmbedding (ToFiniteCoproductHomeo X)

end Iso

lemma finiteCoproduct.Î¹_injective (a : Î±) : Function.Injective (finiteCoproduct.Î¹ X a) := by
  intro x y hxy
  exact eq_of_heq (Sigma.ext_iff.mp hxy).2

lemma finiteCoproduct.Î¹_jointly_surjective (R : finiteCoproduct X) :
    âˆƒ (a : Î±) (r : X a), R = finiteCoproduct.Î¹ X a r := by
  exact âŸ¨R.fst, R.snd, rflâŸ©

lemma finiteCoproduct.Î¹_desc_apply {B : CompHaus} {Ï€ : (a : Î±) â†’ X a âŸ¶ B} (a : Î±) :
    âˆ€ x, finiteCoproduct.desc X Ï€ (finiteCoproduct.Î¹ X a x) = Ï€ a x := by
  intro x
  change (Î¹ X a â‰« desc X Ï€) _ = _
  simp only [Î¹_desc]

end FiniteCoproducts

end CompHaus
