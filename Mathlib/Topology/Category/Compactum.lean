/-
Copyright (c) 2020 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/
import Mathlib.CategoryTheory.Monad.Types
import Mathlib.CategoryTheory.Monad.Limits
import Mathlib.CategoryTheory.Equivalence
import Mathlib.Topology.Category.CompHaus.Basic
import Mathlib.Topology.Category.Profinite.Basic
import Mathlib.Data.Set.Constructions

#align_import topology.category.Compactum from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!

# Compacta and Compact Hausdorff Spaces

Recall that, given a monad `M` on `Type*`, an *algebra* for `M` consists of the following data:
- A type `X : Type*`
- A "structure" map `M X ‚Üí X`.
This data must also satisfy a distributivity and unit axiom, and algebras for `M` form a category
in an evident way.

See the file `CategoryTheory.Monad.Algebra` for a general version, as well as the following link.
https://ncatlab.org/nlab/show/monad

This file proves the equivalence between the category of *compact Hausdorff topological spaces*
and the category of algebras for the *ultrafilter monad*.

## Notation:

Here are the main objects introduced in this file.
- `Compactum` is the type of compacta, which we define as algebras for the ultrafilter monad.
- `compactumToCompHaus` is the functor `Compactum ‚•§ CompHaus`. Here `CompHaus` is the usual
  category of compact Hausdorff spaces.
- `compactumToCompHaus.isEquivalence` is a term of type `IsEquivalence compactumToCompHaus`.

The proof of this equivalence is a bit technical. But the idea is quite simply that the structure
map `Ultrafilter X ‚Üí X` for an algebra `X` of the ultrafilter monad should be considered as the map
sending an ultrafilter to its limit in `X`. The topology on `X` is then defined by mimicking the
characterization of open sets in terms of ultrafilters.

Any `X : Compactum` is endowed with a coercion to `Type*`, as well as the following instances:
- `TopologicalSpace X`.
- `CompactSpace X`.
- `T2Space X`.

Any morphism `f : X ‚ü∂ Y` of is endowed with a coercion to a function `X ‚Üí Y`, which is shown to
be continuous in `continuous_of_hom`.

The function `Compactum.ofTopologicalSpace` can be used to construct a `Compactum` from a
topological space which satisfies `CompactSpace` and `T2Space`.

We also add wrappers around structures which already exist. Here are the main ones, all in the
`Compactum` namespace:

- `forget : Compactum ‚•§ Type*` is the forgetful functor, which induces a `ConcreteCategory`
  instance for `Compactum`.
- `free : Type* ‚•§ Compactum` is the left adjoint to `forget`, and the adjunction is in `adj`.
- `str : Ultrafilter X ‚Üí X` is the structure map for `X : Compactum`.
  The notation `X.str` is preferred.
- `join : Ultrafilter (Ultrafilter X) ‚Üí Ultrafilter X` is the monadic join for `X : Compactum`.
  Again, the notation `X.join` is preferred.
- `incl : X ‚Üí Ultrafilter X` is the unit for `X : Compactum`. The notation `X.incl` is preferred.

## References

- E. Manes, Algebraic Theories, Graduate Texts in Mathematics 26, Springer-Verlag, 1976.
- https://ncatlab.org/nlab/show/ultrafilter

-/

-- Porting note: "Compactum" is already upper case
set_option linter.uppercaseLean3 false
universe u

open CategoryTheory Filter Ultrafilter TopologicalSpace CategoryTheory.Limits FiniteInter

open scoped Classical
open Topology

local notation "Œ≤" => ofTypeMonad Ultrafilter

/-- The type `Compactum` of Compacta, defined as algebras for the ultrafilter monad. -/
def Compactum :=
  Monad.Algebra Œ≤ deriving Category, Inhabited
#align Compactum Compactum

namespace Compactum

/-- The forgetful functor to Type* -/
def forget : Compactum ‚•§ Type* :=
  Monad.forget _ --deriving CreatesLimits, Faithful
  -- Porting note: deriving fails, adding manually. Note `CreatesLimits` now noncomputable
#align Compactum.forget Compactum.forget

instance : forget.Faithful :=
  show (Monad.forget _).Faithful from inferInstance

noncomputable instance : CreatesLimits forget :=
  show CreatesLimits <| Monad.forget _ from inferInstance

/-- The "free" Compactum functor. -/
def free : Type* ‚•§ Compactum :=
  Monad.free _
#align Compactum.free Compactum.free

/-- The adjunction between `free` and `forget`. -/
def adj : free ‚ä£ forget :=
  Monad.adj _
#align Compactum.adj Compactum.adj

-- Basic instances
instance : ConcreteCategory Compactum where forget := forget

-- Porting note: changed from forget to X.A
instance : CoeSort Compactum Type* :=
  ‚ü®fun X => X.A‚ü©

instance {X Y : Compactum} : CoeFun (X ‚ü∂ Y) fun _ => X ‚Üí Y :=
  ‚ü®fun f => f.f‚ü©

instance : HasLimits Compactum :=
  hasLimits_of_hasLimits_createsLimits forget

/-- The structure map for a compactum, essentially sending an ultrafilter to its limit. -/
def str (X : Compactum) : Ultrafilter X ‚Üí X :=
  X.a
#align Compactum.str Compactum.str

/-- The monadic join. -/
def join (X : Compactum) : Ultrafilter (Ultrafilter X) ‚Üí Ultrafilter X :=
  (Œ≤ ).Œº.app _
#align Compactum.join Compactum.join

/-- The inclusion of `X` into `Ultrafilter X`. -/
def incl (X : Compactum) : X ‚Üí Ultrafilter X :=
  (Œ≤ ).Œ∑.app _
#align Compactum.incl Compactum.incl

@[simp]
theorem str_incl (X : Compactum) (x : X) : X.str (X.incl x) = x := by
  change ((Œ≤ ).Œ∑.app _ ‚â´ X.a) _ = _
  rw [Monad.Algebra.unit]
  rfl
#align Compactum.str_incl Compactum.str_incl

@[simp]
theorem str_hom_commute (X Y : Compactum) (f : X ‚ü∂ Y) (xs : Ultrafilter X) :
    f (X.str xs) = Y.str (map f xs) := by
  change (X.a ‚â´ f.f) _ = _
  rw [‚Üê f.h]
  rfl
#align Compactum.str_hom_commute Compactum.str_hom_commute

@[simp]
theorem join_distrib (X : Compactum) (uux : Ultrafilter (Ultrafilter X)) :
    X.str (X.join uux) = X.str (map X.str uux) := by
  change ((Œ≤ ).Œº.app _ ‚â´ X.a) _ = _
  rw [Monad.Algebra.assoc]
  rfl
#align Compactum.join_distrib Compactum.join_distrib

-- Porting note: changes to X.A from X since Lean can't see through X to X.A below
instance {X : Compactum} : TopologicalSpace X.A where
  IsOpen U := ‚àÄ F : Ultrafilter X, X.str F ‚àà U ‚Üí U ‚àà F
  isOpen_univ _ _ := Filter.univ_sets _
  isOpen_inter _ _ h3 h4 _ h6 := Filter.inter_sets _ (h3 _ h6.1) (h4 _ h6.2)
  isOpen_sUnion := fun _ h1 _ ‚ü®T, hT, h2‚ü© =>
    mem_of_superset (h1 T hT _ h2) (Set.subset_sUnion_of_mem hT)

theorem isClosed_iff {X : Compactum} (S : Set X) :
    IsClosed S ‚Üî ‚àÄ F : Ultrafilter X, S ‚àà F ‚Üí X.str F ‚àà S := by
  rw [‚Üê isOpen_compl_iff]
  constructor
  ¬∑ intro cond F h
    by_contra c
    specialize cond F c
    rw [compl_mem_iff_not_mem] at cond
    contradiction
  ¬∑ intro h1 F h2
    specialize h1 F
    cases' F.mem_or_compl_mem S with h h
    exacts [absurd (h1 h) h2, h]
#align Compactum.is_closed_iff Compactum.isClosed_iff

instance {X : Compactum} : CompactSpace X := by
  constructor
  rw [isCompact_iff_ultrafilter_le_nhds]
  intro F _
  refine ‚ü®X.str F, by tauto, ?_‚ü©
  rw [le_nhds_iff]
  intro S h1 h2
  exact h2 F h1

/-- A local definition used only in the proofs. -/
private def basic {X : Compactum} (A : Set X) : Set (Ultrafilter X) :=
  { F | A ‚àà F }

/-- A local definition used only in the proofs. -/
private def cl {X : Compactum} (A : Set X) : Set X :=
  X.str '' basic A

private theorem basic_inter {X : Compactum} (A B : Set X) : basic (A ‚à© B) = basic A ‚à© basic B := by
  ext G
  constructor
  ¬∑ intro hG
    constructor <;> filter_upwards [hG] with _
    exacts [And.left, And.right]
  ¬∑ rintro ‚ü®h1, h2‚ü©
    exact inter_mem h1 h2

private theorem subset_cl {X : Compactum} (A : Set X) : A ‚äÜ cl A := fun a ha =>
  ‚ü®X.incl a, ha, by simp‚ü©

private theorem cl_cl {X : Compactum} (A : Set X) : cl (cl A) ‚äÜ cl A := by
  rintro _ ‚ü®F, hF, rfl‚ü©
  -- Notation to be used in this proof.
  let fsu := Finset (Set (Ultrafilter X))
  let ssu := Set (Set (Ultrafilter X))
  let Œπ : fsu ‚Üí ssu := fun x ‚Ü¶ ‚Üëx
  let C0 : ssu := { Z | ‚àÉ B ‚àà F, X.str ‚Åª¬π' B = Z }
  let AA := { G : Ultrafilter X | A ‚àà G }
  let C1 := insert AA C0
  let C2 := finiteInterClosure C1
  -- C0 is closed under intersections.
  have claim1 : ‚àÄ (B) (_ : B ‚àà C0) (C) (_ : C ‚àà C0), B ‚à© C ‚àà C0 := by
    rintro B ‚ü®Q, hQ, rfl‚ü© C ‚ü®R, hR, rfl‚ü©
    use Q ‚à© R
    simp only [and_true_iff, eq_self_iff_true, Set.preimage_inter]
    exact inter_sets _ hQ hR
  -- All sets in C0 are nonempty.
  have claim2 : ‚àÄ B ‚àà C0, Set.Nonempty B := by
    rintro B ‚ü®Q, hQ, rfl‚ü©
    obtain ‚ü®q‚ü© := Filter.nonempty_of_mem hQ
    use X.incl q
    simpa
  -- The intersection of AA with every set in C0 is nonempty.
  have claim3 : ‚àÄ B ‚àà C0, (AA ‚à© B).Nonempty := by
    rintro B ‚ü®Q, hQ, rfl‚ü©
    have : (Q ‚à© cl A).Nonempty := Filter.nonempty_of_mem (inter_mem hQ hF)
    rcases this with ‚ü®q, hq1, P, hq2, hq3‚ü©
    refine ‚ü®P, hq2, ?_‚ü©
    rw [‚Üê hq3] at hq1
    simpa
  -- Suffices to show that the intersection of any finite subcollection of C1 is nonempty.
  suffices ‚àÄ T : fsu, Œπ T ‚äÜ C1 ‚Üí (‚ãÇ‚ÇÄ Œπ T).Nonempty by
    obtain ‚ü®G, h1‚ü© := exists_ultrafilter_of_finite_inter_nonempty _ this
    use X.join G
    have : G.map X.str = F := Ultrafilter.coe_le_coe.1 fun S hS => h1 (Or.inr ‚ü®S, hS, rfl‚ü©)
    rw [join_distrib, this]
    exact ‚ü®h1 (Or.inl rfl), rfl‚ü©
  -- C2 is closed under finite intersections (by construction!).
  have claim4 := finiteInterClosure_finiteInter C1
  -- C0 is closed under finite intersections by claim1.
  have claim5 : FiniteInter C0 := ‚ü®‚ü®_, univ_mem, Set.preimage_univ‚ü©, claim1‚ü©
  -- Every element of C2 is nonempty.
  have claim6 : ‚àÄ P ‚àà C2, (P : Set (Ultrafilter X)).Nonempty := by
    suffices ‚àÄ P ‚àà C2, P ‚àà C0 ‚à® ‚àÉ Q ‚àà C0, P = AA ‚à© Q by
      intro P hP
      cases' this P hP with h h
      ¬∑ exact claim2 _ h
      ¬∑ rcases h with ‚ü®Q, hQ, rfl‚ü©
        exact claim3 _ hQ
    intro P hP
    exact claim5.finiteInterClosure_insert _ hP
  intro T hT
  -- Suffices to show that the intersection of the T's is contained in C2.
  suffices ‚ãÇ‚ÇÄ Œπ T ‚àà C2 by exact claim6 _ this
  -- Finish
  apply claim4.finiteInter_mem T
  intro t ht
  exact finiteInterClosure.basic (@hT t ht)

theorem isClosed_cl {X : Compactum} (A : Set X) : IsClosed (cl A) := by
  rw [isClosed_iff]
  intro F hF
  exact cl_cl _ ‚ü®F, hF, rfl‚ü©
#align Compactum.is_closed_cl Compactum.isClosed_cl

theorem str_eq_of_le_nhds {X : Compactum} (F : Ultrafilter X) (x : X) : ‚ÜëF ‚â§ ùìù x ‚Üí X.str F = x := by
  -- Notation to be used in this proof.
  let fsu := Finset (Set (Ultrafilter X))
  let ssu := Set (Set (Ultrafilter X))
  let Œπ : fsu ‚Üí ssu := fun x ‚Ü¶ ‚Üëx
  let T0 : ssu := { S | ‚àÉ A ‚àà F, S = basic A }
  let AA := X.str ‚Åª¬π' {x}
  let T1 := insert AA T0
  let T2 := finiteInterClosure T1
  intro cond
  -- If F contains a closed set A, then x is contained in A.
  have claim1 : ‚àÄ A : Set X, IsClosed A ‚Üí A ‚àà F ‚Üí x ‚àà A := by
    intro A hA h
    by_contra H
    rw [le_nhds_iff] at cond
    specialize cond A·∂ú H hA.isOpen_compl
    rw [Ultrafilter.mem_coe, Ultrafilter.compl_mem_iff_not_mem] at cond
    contradiction
  -- If A ‚àà F, then x ‚àà cl A.
  have claim2 : ‚àÄ A : Set X, A ‚àà F ‚Üí x ‚àà cl A := by
    intro A hA
    exact claim1 (cl A) (isClosed_cl A) (mem_of_superset hA (subset_cl A))
  -- T0 is closed under intersections.
  have claim3 : ‚àÄ (S1) (_ : S1 ‚àà T0) (S2) (_ : S2 ‚àà T0), S1 ‚à© S2 ‚àà T0 := by
    rintro S1 ‚ü®S1, hS1, rfl‚ü© S2 ‚ü®S2, hS2, rfl‚ü©
    exact ‚ü®S1 ‚à© S2, inter_mem hS1 hS2, by simp [basic_inter]‚ü©
  -- For every S ‚àà T0, the intersection AA ‚à© S is nonempty.
  have claim4 : ‚àÄ S ‚àà T0, (AA ‚à© S).Nonempty := by
    rintro S ‚ü®S, hS, rfl‚ü©
    rcases claim2 _ hS with ‚ü®G, hG, hG2‚ü©
    exact ‚ü®G, hG2, hG‚ü©
  -- Every element of T0 is nonempty.
  have claim5 : ‚àÄ S ‚àà T0, Set.Nonempty S := by
    rintro S ‚ü®S, hS, rfl‚ü©
    exact ‚ü®F, hS‚ü©
  -- Every element of T2 is nonempty.
  have claim6 : ‚àÄ S ‚àà T2, Set.Nonempty S := by
    suffices ‚àÄ S ‚àà T2, S ‚àà T0 ‚à® ‚àÉ Q ‚àà T0, S = AA ‚à© Q by
      intro S hS
      cases' this _ hS with h h
      ¬∑ exact claim5 S h
      ¬∑ rcases h with ‚ü®Q, hQ, rfl‚ü©
        exact claim4 Q hQ
    intro S hS
    apply finiteInterClosure_insert
    ¬∑ constructor
      ¬∑ use Set.univ
        refine ‚ü®Filter.univ_sets _, ?_‚ü©
        ext
        refine ‚ü®?_, by tauto‚ü©
        ¬∑ intro
          apply Filter.univ_sets
      ¬∑ exact claim3
    ¬∑ exact hS
  -- It suffices to show that the intersection of any finite subset of T1 is nonempty.
  suffices ‚àÄ F : fsu, ‚ÜëF ‚äÜ T1 ‚Üí (‚ãÇ‚ÇÄ Œπ F).Nonempty by
    obtain ‚ü®G, h1‚ü© := Ultrafilter.exists_ultrafilter_of_finite_inter_nonempty _ this
    have c1 : X.join G = F := Ultrafilter.coe_le_coe.1 fun P hP => h1 (Or.inr ‚ü®P, hP, rfl‚ü©)
    have c2 : G.map X.str = X.incl x := by
      refine Ultrafilter.coe_le_coe.1 fun P hP => ?_
      apply mem_of_superset (h1 (Or.inl rfl))
      rintro x ‚ü®rfl‚ü©
      exact hP
    simp [‚Üê c1, c2]
  -- Finish...
  intro T hT
  refine claim6 _ (finiteInter_mem (.finiteInterClosure_finiteInter _) _ ?_)
  intro t ht
  exact finiteInterClosure.basic (@hT t ht)
#align Compactum.str_eq_of_le_nhds Compactum.str_eq_of_le_nhds

theorem le_nhds_of_str_eq {X : Compactum} (F : Ultrafilter X) (x : X) : X.str F = x ‚Üí ‚ÜëF ‚â§ ùìù x :=
  fun h => le_nhds_iff.mpr fun s hx hs => hs _ <| by rwa [h]
#align Compactum.le_nhds_of_str_eq Compactum.le_nhds_of_str_eq

-- All the hard work above boils down to this `T2Space` instance.
instance {X : Compactum} : T2Space X := by
  rw [t2_iff_ultrafilter]
  intro _ _ F hx hy
  rw [‚Üê str_eq_of_le_nhds _ _ hx, ‚Üê str_eq_of_le_nhds _ _ hy]

/-- The structure map of a compactum actually computes limits. -/
theorem lim_eq_str {X : Compactum} (F : Ultrafilter X) : F.lim = X.str F := by
  rw [Ultrafilter.lim_eq_iff_le_nhds, le_nhds_iff]
  tauto
#align Compactum.Lim_eq_str Compactum.lim_eq_str

theorem cl_eq_closure {X : Compactum} (A : Set X) : cl A = closure A := by
  ext
  rw [mem_closure_iff_ultrafilter]
  constructor
  ¬∑ rintro ‚ü®F, h1, h2‚ü©
    exact ‚ü®F, h1, le_nhds_of_str_eq _ _ h2‚ü©
  ¬∑ rintro ‚ü®F, h1, h2‚ü©
    exact ‚ü®F, h1, str_eq_of_le_nhds _ _ h2‚ü©
#align Compactum.cl_eq_closure Compactum.cl_eq_closure

/-- Any morphism of compacta is continuous. -/
theorem continuous_of_hom {X Y : Compactum} (f : X ‚ü∂ Y) : Continuous f := by
  rw [continuous_iff_ultrafilter]
  intro x g h
  rw [Tendsto, ‚Üê coe_map]
  apply le_nhds_of_str_eq
  rw [‚Üê str_hom_commute, str_eq_of_le_nhds _ x _]
  apply h
#align Compactum.continuous_of_hom Compactum.continuous_of_hom

/-- Given any compact Hausdorff space, we construct a Compactum. -/
noncomputable def ofTopologicalSpace (X : Type*) [TopologicalSpace X] [CompactSpace X]
    [T2Space X] : Compactum where
  A := X
  a := Ultrafilter.lim
  unit := by
    ext x
    exact lim_eq (pure_le_nhds _)
  assoc := by
    ext FF
    change Ultrafilter (Ultrafilter X) at FF
    set x := (Ultrafilter.map Ultrafilter.lim FF).lim with c1
    have c2 : ‚àÄ (U : Set X) (F : Ultrafilter X), F.lim ‚àà U ‚Üí IsOpen U ‚Üí U ‚àà F := by
      intro U F h1 hU
      exact isOpen_iff_ultrafilter.mp hU _ h1 _ (Ultrafilter.le_nhds_lim _)
    have c3 : ‚Üë(Ultrafilter.map Ultrafilter.lim FF) ‚â§ ùìù x := by
      rw [le_nhds_iff]
      intro U hx hU
      exact mem_coe.2 (c2 _ _ (by rwa [‚Üê c1]) hU)
    have c4 : ‚àÄ U : Set X, x ‚àà U ‚Üí IsOpen U ‚Üí { G : Ultrafilter X | U ‚àà G } ‚àà FF := by
      intro U hx hU
      suffices Ultrafilter.lim ‚Åª¬π' U ‚àà FF by
        apply mem_of_superset this
        intro P hP
        exact c2 U P hP hU
      exact @c3 U (IsOpen.mem_nhds hU hx)
    apply lim_eq
    rw [le_nhds_iff]
    exact c4
#align Compactum.of_topological_space Compactum.ofTopologicalSpace

/-- Any continuous map between Compacta is a morphism of compacta. -/
def homOfContinuous {X Y : Compactum} (f : X ‚Üí Y) (cont : Continuous f) : X ‚ü∂ Y :=
  { f
    h := by
      rw [continuous_iff_ultrafilter] at cont
      ext (F : Ultrafilter X)
      specialize cont (X.str F) F (le_nhds_of_str_eq F (X.str F) rfl)
      simp only [types_comp_apply, ofTypeFunctor_map]
      exact str_eq_of_le_nhds (Ultrafilter.map f F) _ cont }
#align Compactum.hom_of_continuous Compactum.homOfContinuous

end Compactum

/-- The functor functor from Compactum to CompHaus. -/
def compactumToCompHaus : Compactum ‚•§ CompHaus where
  obj X := { toTop := { Œ± := X } }
  map := @fun X Y f =>
    { toFun := f
      continuous_toFun := Compactum.continuous_of_hom _ }
#align Compactum_to_CompHaus compactumToCompHaus

namespace compactumToCompHaus

/-- The functor `compactumToCompHaus` is full. -/
instance full : compactumToCompHaus.{u}.Full where
  map_surjective f := ‚ü®Compactum.homOfContinuous f.1 f.2, rfl‚ü©
#align Compactum_to_CompHaus.full compactumToCompHaus.full

/-- The functor `compactumToCompHaus` is faithful. -/
instance faithful : compactumToCompHaus.Faithful where
  -- Porting note: this used to be obviously (though it consumed a bit of memory)
  map_injective := by
    intro _ _ _ _ h
    -- Porting note: ext gets confused by coercion using forget
    apply Monad.Algebra.Hom.ext
    apply congrArg (fun f => f.toFun) h
#align Compactum_to_CompHaus.faithful compactumToCompHaus.faithful

/-- This definition is used to prove essential surjectivity of `compactumToCompHaus`. -/
def isoOfTopologicalSpace {D : CompHaus} :
    compactumToCompHaus.obj (Compactum.ofTopologicalSpace D) ‚âÖ D where
  hom :=
    { toFun := id
      continuous_toFun :=
        continuous_def.2 fun _ h => by
          rw [isOpen_iff_ultrafilter'] at h
          exact h }
  inv :=
    { toFun := id
      continuous_toFun :=
        continuous_def.2 fun _ h1 => by
          rw [isOpen_iff_ultrafilter']
          intro _ h2
          exact h1 _ h2 }
#align Compactum_to_CompHaus.iso_of_topological_space compactumToCompHaus.isoOfTopologicalSpace

/-- The functor `compactumToCompHaus` is essentially surjective. -/
instance essSurj : compactumToCompHaus.EssSurj :=
  { mem_essImage := fun X => ‚ü®Compactum.ofTopologicalSpace X, ‚ü®isoOfTopologicalSpace‚ü©‚ü© }
#align Compactum_to_CompHaus.ess_surj compactumToCompHaus.essSurj

/-- The functor `compactumToCompHaus` is an equivalence of categories. -/
instance isEquivalence : compactumToCompHaus.IsEquivalence where
#align Compactum_to_CompHaus.is_equivalence compactumToCompHaus.isEquivalence

end compactumToCompHaus

/-- The forgetful functors of `Compactum` and `CompHaus` are compatible via
`compactumToCompHaus`. -/
def compactumToCompHausCompForget :
    compactumToCompHaus ‚ãô CategoryTheory.forget CompHaus ‚âÖ Compactum.forget :=
  NatIso.ofComponents fun X => eqToIso rfl
#align Compactum_to_CompHaus_comp_forget compactumToCompHausCompForget

/-
TODO: `forget CompHaus` is monadic, as it is isomorphic to the composition
of an equivalence with the monadic functor `forget Compactum`.
Once we have the API to transfer monadicity of functors along such isomorphisms,
the instance `CreatesLimits (forget CompHaus)` can be deduced from this
monadicity.
-/
noncomputable instance CompHaus.forgetCreatesLimits : CreatesLimits (forget CompHaus) := by
  let e : forget CompHaus ‚âÖ compactumToCompHaus.inv ‚ãô Compactum.forget :=
    (((forget CompHaus).leftUnitor.symm ‚â™‚â´
    isoWhiskerRight compactumToCompHaus.asEquivalence.symm.unitIso (forget CompHaus)) ‚â™‚â´
    compactumToCompHaus.inv.associator compactumToCompHaus (forget CompHaus)) ‚â™‚â´
    isoWhiskerLeft _ compactumToCompHausCompForget
  exact createsLimitsOfNatIso e.symm

#align CompHaus.forget_creates_limits CompHaus.forgetCreatesLimits

noncomputable instance Profinite.forgetCreatesLimits : CreatesLimits (forget Profinite) := by
  change CreatesLimits (profiniteToCompHaus ‚ãô forget _)
  infer_instance
#align Profinite.forget_creates_limits Profinite.forgetCreatesLimits
