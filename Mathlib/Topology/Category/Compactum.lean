/-
Copyright (c) 2020 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/
import Mathlib.CategoryTheory.Monad.Types
import Mathlib.CategoryTheory.Monad.Limits
import Mathlib.CategoryTheory.Equivalence
import Mathlib.Topology.Category.CompHaus.Basic
import Mathlib.Topology.Category.Profinite.Basic
import Mathlib.Data.Set.Constructions

#align_import topology.category.Compactum from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!

# Compacta and Compact Hausdorff Spaces

Recall that, given a monad `M` on `Type*`, an *algebra* for `M` consists of the following data:
- A type `X : Type*`
- A "structure" map `M X â†’ X`.
This data must also satisfy a distributivity and unit axiom, and algebras for `M` form a category
in an evident way.

See the file `CategoryTheory.Monad.Algebra` for a general version, as well as the following link.
https://ncatlab.org/nlab/show/monad

This file proves the equivalence between the category of *compact Hausdorff topological spaces*
and the category of algebras for the *ultrafilter monad*.

## Notation:

Here are the main objects introduced in this file.
- `Compactum` is the type of compacta, which we define as algebras for the ultrafilter monad.
- `compactumToCompHaus` is the functor `Compactum â¥¤ CompHaus`. Here `CompHaus` is the usual
  category of compact Hausdorff spaces.
- `compactumToCompHaus.isEquivalence` is a term of type `IsEquivalence compactumToCompHaus`.

The proof of this equivalence is a bit technical. But the idea is quite simply that the structure
map `Ultrafilter X â†’ X` for an algebra `X` of the ultrafilter monad should be considered as the map
sending an ultrafilter to its limit in `X`. The topology on `X` is then defined by mimicking the
characterization of open sets in terms of ultrafilters.

Any `X : Compactum` is endowed with a coercion to `Type*`, as well as the following instances:
- `TopologicalSpace X`.
- `CompactSpace X`.
- `T2Space X`.

Any morphism `f : X âŸ¶ Y` of is endowed with a coercion to a function `X â†’ Y`, which is shown to
be continuous in `continuous_of_hom`.

The function `Compactum.ofTopologicalSpace` can be used to construct a `Compactum` from a
topological space which satisfies `CompactSpace` and `T2Space`.

We also add wrappers around structures which already exist. Here are the main ones, all in the
`Compactum` namespace:

- `forget : Compactum â¥¤ Type*` is the forgetful functor, which induces a `ConcreteCategory`
  instance for `Compactum`.
- `free : Type* â¥¤ Compactum` is the left adjoint to `forget`, and the adjunction is in `adj`.
- `str : Ultrafilter X â†’ X` is the structure map for `X : Compactum`.
  The notation `X.str` is preferred.
- `join : Ultrafilter (Ultrafilter X) â†’ Ultrafilter X` is the monadic join for `X : Compactum`.
  Again, the notation `X.join` is preferred.
- `incl : X â†’ Ultrafilter X` is the unit for `X : Compactum`. The notation `X.incl` is preferred.

## References

- E. Manes, Algebraic Theories, Graduate Texts in Mathematics 26, Springer-Verlag, 1976.
- https://ncatlab.org/nlab/show/ultrafilter

-/

-- porting note: "Compactum" is already upper case
set_option linter.uppercaseLean3 false
universe u

open CategoryTheory Filter Ultrafilter TopologicalSpace CategoryTheory.Limits FiniteInter

open Classical Topology

local notation "Î²" => ofTypeMonad Ultrafilter

/-- The type `Compactum` of Compacta, defined as algebras for the ultrafilter monad. -/
def Compactum :=
  Monad.Algebra Î² deriving Category, Inhabited
#align Compactum Compactum

namespace Compactum

/-- The forgetful functor to Type* -/
def forget : Compactum â¥¤ Type* :=
  Monad.forget _ --deriving CreatesLimits, Faithful
  -- Porting note: deriving fails, adding manually. Note `CreatesLimits` now noncomputable
#align Compactum.forget Compactum.forget

instance : Faithful forget :=
  show Faithful <| Monad.forget _ from inferInstance

noncomputable instance : CreatesLimits forget :=
  show CreatesLimits <| Monad.forget _ from inferInstance

/-- The "free" Compactum functor. -/
def free : Type* â¥¤ Compactum :=
  Monad.free _
#align Compactum.free Compactum.free

/-- The adjunction between `free` and `forget`. -/
def adj : free âŠ£ forget :=
  Monad.adj _
#align Compactum.adj Compactum.adj

-- Basic instances
instance : ConcreteCategory Compactum where forget := forget

-- Porting note: changed from forget to X.A
instance : CoeSort Compactum (Type*) :=
  âŸ¨fun X => X.AâŸ©

instance {X Y : Compactum} : CoeFun (X âŸ¶ Y) fun _ => X â†’ Y :=
  âŸ¨fun f => f.fâŸ©

instance : HasLimits Compactum :=
  hasLimits_of_hasLimits_createsLimits forget

/-- The structure map for a compactum, essentially sending an ultrafilter to its limit. -/
def str (X : Compactum) : Ultrafilter X â†’ X :=
  X.a
#align Compactum.str Compactum.str

/-- The monadic join. -/
def join (X : Compactum) : Ultrafilter (Ultrafilter X) â†’ Ultrafilter X :=
  (Î² ).Î¼.app _
#align Compactum.join Compactum.join

/-- The inclusion of `X` into `Ultrafilter X`. -/
def incl (X : Compactum) : X â†’ Ultrafilter X :=
  (Î² ).Î·.app _
#align Compactum.incl Compactum.incl

@[simp]
theorem str_incl (X : Compactum) (x : X) : X.str (X.incl x) = x := by
  change ((Î² ).Î·.app _ â‰« X.a) _ = _
  -- âŠ¢ (NatTrans.app (Monad.Î· Î²) X.A â‰« X.a) x = x
  rw [Monad.Algebra.unit]
  -- âŠ¢ ğŸ™ X.A x = x
  rfl
  -- ğŸ‰ no goals
#align Compactum.str_incl Compactum.str_incl

@[simp]
theorem str_hom_commute (X Y : Compactum) (f : X âŸ¶ Y) (xs : Ultrafilter X) :
    f (X.str xs) = Y.str (map f xs) := by
  change (X.a â‰« f.f) _ = _
  -- âŠ¢ (X.a â‰« f.f) xs = str Y (Ultrafilter.map f.f xs)
  rw [â† f.h]
  -- âŠ¢ (Î².toFunctor.map f.f â‰« Y.a) xs = str Y (Ultrafilter.map f.f xs)
  rfl
  -- ğŸ‰ no goals
#align Compactum.str_hom_commute Compactum.str_hom_commute

@[simp]
theorem join_distrib (X : Compactum) (uux : Ultrafilter (Ultrafilter X)) :
    X.str (X.join uux) = X.str (map X.str uux) := by
  change ((Î² ).Î¼.app _ â‰« X.a) _ = _
  -- âŠ¢ (NatTrans.app (Monad.Î¼ Î²) X.A â‰« X.a) uux = str X (Ultrafilter.map (str X) uux)
  rw [Monad.Algebra.assoc]
  -- âŠ¢ (Î².toFunctor.map X.a â‰« X.a) uux = str X (Ultrafilter.map (str X) uux)
  rfl
  -- ğŸ‰ no goals
#align Compactum.join_distrib Compactum.join_distrib

-- Porting note: changes to X.A from X since Lean can't see through X to X.A below
instance {X : Compactum} : TopologicalSpace X.A where
  IsOpen U := âˆ€ F : Ultrafilter X, X.str F âˆˆ U â†’ U âˆˆ F
  isOpen_univ _ _ := Filter.univ_sets _
  isOpen_inter _ _ h3 h4 _ h6 := Filter.inter_sets _ (h3 _ h6.1) (h4 _ h6.2)
  isOpen_sUnion := fun _ h1 _ âŸ¨T, hT, h2âŸ© =>
    mem_of_superset (h1 T hT _ h2) (Set.subset_sUnion_of_mem hT)

theorem isClosed_iff {X : Compactum} (S : Set X) :
    IsClosed S â†” âˆ€ F : Ultrafilter X, S âˆˆ F â†’ X.str F âˆˆ S := by
  rw [â† isOpen_compl_iff]
  -- âŠ¢ IsOpen Sá¶œ â†” âˆ€ (F : Ultrafilter X.A), S âˆˆ F â†’ str X F âˆˆ S
  constructor
  -- âŠ¢ IsOpen Sá¶œ â†’ âˆ€ (F : Ultrafilter X.A), S âˆˆ F â†’ str X F âˆˆ S
  Â· intro cond F h
    -- âŠ¢ str X F âˆˆ S
    by_contra c
    -- âŠ¢ False
    specialize cond F c
    -- âŠ¢ False
    rw [compl_mem_iff_not_mem] at cond
    -- âŠ¢ False
    contradiction
    -- ğŸ‰ no goals
  Â· intro h1 F h2
    -- âŠ¢ Sá¶œ âˆˆ F
    specialize h1 F
    -- âŠ¢ Sá¶œ âˆˆ F
    cases' F.mem_or_compl_mem S with h h
    -- âŠ¢ Sá¶œ âˆˆ F
    exacts [absurd (h1 h) h2, h]
    -- ğŸ‰ no goals
#align Compactum.is_closed_iff Compactum.isClosed_iff

instance {X : Compactum} : CompactSpace X := by
  constructor
  -- âŠ¢ IsCompact Set.univ
  rw [isCompact_iff_ultrafilter_le_nhds]
  -- âŠ¢ âˆ€ (f : Ultrafilter X.A), â†‘f â‰¤ ğ“Ÿ Set.univ â†’ âˆƒ a, a âˆˆ Set.univ âˆ§ â†‘f â‰¤ ğ“ a
  intro F _
  -- âŠ¢ âˆƒ a, a âˆˆ Set.univ âˆ§ â†‘F â‰¤ ğ“ a
  refine' âŸ¨X.str F, by tauto, _âŸ©
  -- âŠ¢ â†‘F â‰¤ ğ“ (str X F)
  rw [le_nhds_iff]
  -- âŠ¢ âˆ€ (s : Set X.A), str X F âˆˆ s â†’ IsOpen s â†’ s âˆˆ â†‘F
  intro S h1 h2
  -- âŠ¢ S âˆˆ â†‘F
  exact h2 F h1
  -- ğŸ‰ no goals

/-- A local definition used only in the proofs. -/
private def basic {X : Compactum} (A : Set X) : Set (Ultrafilter X) :=
  { F | A âˆˆ F }

/-- A local definition used only in the proofs. -/
private def cl {X : Compactum} (A : Set X) : Set X :=
  X.str '' basic A

private theorem basic_inter {X : Compactum} (A B : Set X) : basic (A âˆ© B) = basic A âˆ© basic B := by
  ext G
  -- âŠ¢ G âˆˆ Compactum.basic (A âˆ© B) â†” G âˆˆ Compactum.basic A âˆ© Compactum.basic B
  constructor
  -- âŠ¢ G âˆˆ Compactum.basic (A âˆ© B) â†’ G âˆˆ Compactum.basic A âˆ© Compactum.basic B
  Â· intro hG
    -- âŠ¢ G âˆˆ Compactum.basic A âˆ© Compactum.basic B
    constructor <;> filter_upwards [hG]with _
    -- âŠ¢ G âˆˆ Compactum.basic A
                    -- âŠ¢ aâœ âˆˆ A âˆ© B â†’ aâœ âˆˆ A
                    -- âŠ¢ aâœ âˆˆ A âˆ© B â†’ aâœ âˆˆ B
    exacts [And.left, And.right]
    -- ğŸ‰ no goals
  Â· rintro âŸ¨h1, h2âŸ©
    -- âŠ¢ G âˆˆ Compactum.basic (A âˆ© B)
    exact inter_mem h1 h2
    -- ğŸ‰ no goals

private theorem subset_cl {X : Compactum} (A : Set X) : A âŠ† cl A := fun a ha =>
  âŸ¨X.incl a, ha, by simpâŸ©
                    -- ğŸ‰ no goals

private theorem cl_cl {X : Compactum} (A : Set X) : cl (cl A) âŠ† cl A := by
  rintro _ âŸ¨F, hF, rflâŸ©
  -- âŠ¢ str X F âˆˆ Compactum.cl A
  -- Notation to be used in this proof.
  let fsu := Finset (Set (Ultrafilter X))
  -- âŠ¢ str X F âˆˆ Compactum.cl A
  let ssu := Set (Set (Ultrafilter X))
  -- âŠ¢ str X F âˆˆ Compactum.cl A
  let Î¹ : fsu â†’ ssu := fun x â†¦ â†‘x
  -- âŠ¢ str X F âˆˆ Compactum.cl A
  let C0 : ssu := { Z | âˆƒ B âˆˆ F, X.str â»Â¹' B = Z }
  -- âŠ¢ str X F âˆˆ Compactum.cl A
  let AA := { G : Ultrafilter X | A âˆˆ G }
  -- âŠ¢ str X F âˆˆ Compactum.cl A
  let C1 := insert AA C0
  -- âŠ¢ str X F âˆˆ Compactum.cl A
  let C2 := finiteInterClosure C1
  -- âŠ¢ str X F âˆˆ Compactum.cl A
  -- C0 is closed under intersections.
  have claim1 : âˆ€ (B) (_ : B âˆˆ C0) (C) (_ : C âˆˆ C0), B âˆ© C âˆˆ C0 := by
    rintro B âŸ¨Q, hQ, rflâŸ© C âŸ¨R, hR, rflâŸ©
    use Q âˆ© R
    simp only [and_true_iff, eq_self_iff_true, Set.preimage_inter]
    exact inter_sets _ hQ hR
  -- All sets in C0 are nonempty.
  have claim2 : âˆ€ B âˆˆ C0, Set.Nonempty B := by
    rintro B âŸ¨Q, hQ, rflâŸ©
    obtain âŸ¨qâŸ© := Filter.nonempty_of_mem hQ
    use X.incl q
    simpa
  -- The intersection of AA with every set in C0 is nonempty.
  have claim3 : âˆ€ B âˆˆ C0, (AA âˆ© B).Nonempty := by
    rintro B âŸ¨Q, hQ, rflâŸ©
    have : (Q âˆ© cl A).Nonempty := Filter.nonempty_of_mem (inter_mem hQ hF)
    rcases this with âŸ¨q, hq1, P, hq2, hq3âŸ©
    refine' âŸ¨P, hq2, _âŸ©
    rw [â† hq3] at hq1
    simpa
  -- Suffices to show that the intersection of any finite subcollection of C1 is nonempty.
  suffices âˆ€ T : fsu, Î¹ T âŠ† C1 â†’ (â‹‚â‚€ Î¹ T).Nonempty by
    obtain âŸ¨G, h1âŸ© := exists_ultrafilter_of_finite_inter_nonempty _ this
    use X.join G
    have : G.map X.str = F := Ultrafilter.coe_le_coe.1 fun S hS => h1 (Or.inr âŸ¨S, hS, rflâŸ©)
    rw [join_distrib, this]
    exact âŸ¨h1 (Or.inl rfl), rflâŸ©
  -- C2 is closed under finite intersections (by construction!).
  have claim4 := finiteInterClosure_finiteInter C1
  -- âŠ¢ âˆ€ (T : fsu), Î¹ T âŠ† C1 â†’ Set.Nonempty (â‹‚â‚€ Î¹ T)
  -- C0 is closed under finite intersections by claim1.
  have claim5 : FiniteInter C0 := âŸ¨âŸ¨_, univ_mem, Set.preimage_univâŸ©, claim1âŸ©
  -- âŠ¢ âˆ€ (T : fsu), Î¹ T âŠ† C1 â†’ Set.Nonempty (â‹‚â‚€ Î¹ T)
  -- Every element of C2 is nonempty.
  have claim6 : âˆ€ P âˆˆ C2, (P : Set (Ultrafilter X)).Nonempty := by
    suffices âˆ€ P âˆˆ C2, P âˆˆ C0 âˆ¨ âˆƒ Q âˆˆ C0, P = AA âˆ© Q by
      intro P hP
      cases' this P hP with h h
      Â· exact claim2 _ h
      Â· rcases h with âŸ¨Q, hQ, rflâŸ©
        exact claim3 _ hQ
    intro P hP
    exact claim5.finiteInterClosure_insert _ hP
  intro T hT
  -- âŠ¢ Set.Nonempty (â‹‚â‚€ Î¹ T)
  -- Suffices to show that the intersection of the T's is contained in C2.
  suffices â‹‚â‚€ Î¹ T âˆˆ C2 by exact claim6 _ this
  -- âŠ¢ â‹‚â‚€ Î¹ T âˆˆ C2
  -- Finish
  apply claim4.finiteInter_mem T
  -- âŠ¢ â†‘T âŠ† finiteInterClosure C1
  intro t ht
  -- âŠ¢ t âˆˆ finiteInterClosure C1
  refine' finiteInterClosure.basic (@hT t ht)
  -- ğŸ‰ no goals

theorem isClosed_cl {X : Compactum} (A : Set X) : IsClosed (cl A) := by
  rw [isClosed_iff]
  -- âŠ¢ âˆ€ (F : Ultrafilter X.A), Compactum.cl A âˆˆ F â†’ str X F âˆˆ Compactum.cl A
  intro F hF
  -- âŠ¢ str X F âˆˆ Compactum.cl A
  exact cl_cl _ âŸ¨F, hF, rflâŸ©
  -- ğŸ‰ no goals
#align Compactum.is_closed_cl Compactum.isClosed_cl

theorem str_eq_of_le_nhds {X : Compactum} (F : Ultrafilter X) (x : X) : â†‘F â‰¤ ğ“ x â†’ X.str F = x := by
  -- Notation to be used in this proof.
  let fsu := Finset (Set (Ultrafilter X))
  -- âŠ¢ â†‘F â‰¤ ğ“ x â†’ str X F = x
  let ssu := Set (Set (Ultrafilter X))
  -- âŠ¢ â†‘F â‰¤ ğ“ x â†’ str X F = x
  let Î¹ : fsu â†’ ssu := fun x â†¦ â†‘x
  -- âŠ¢ â†‘F â‰¤ ğ“ x â†’ str X F = x
  let T0 : ssu := { S | âˆƒ A âˆˆ F, S = basic A }
  -- âŠ¢ â†‘F â‰¤ ğ“ x â†’ str X F = x
  let AA := X.str â»Â¹' {x}
  -- âŠ¢ â†‘F â‰¤ ğ“ x â†’ str X F = x
  let T1 := insert AA T0
  -- âŠ¢ â†‘F â‰¤ ğ“ x â†’ str X F = x
  let T2 := finiteInterClosure T1
  -- âŠ¢ â†‘F â‰¤ ğ“ x â†’ str X F = x
  intro cond
  -- âŠ¢ str X F = x
  -- If F contains a closed set A, then x is contained in A.
  have claim1 : âˆ€ A : Set X, IsClosed A â†’ A âˆˆ F â†’ x âˆˆ A := by
    intro A hA h
    by_contra H
    rw [le_nhds_iff] at cond
    specialize cond Aá¶œ H hA.isOpen_compl
    rw [Ultrafilter.mem_coe, Ultrafilter.compl_mem_iff_not_mem] at cond
    contradiction
  -- If A âˆˆ F, then x âˆˆ cl A.
  have claim2 : âˆ€ A : Set X, A âˆˆ F â†’ x âˆˆ cl A := by
    intro A hA
    exact claim1 (cl A) (isClosed_cl A) (mem_of_superset hA (subset_cl A))
  -- T0 is closed under intersections.
  have claim3 : âˆ€ (S1) (_ : S1 âˆˆ T0) (S2) (_ : S2 âˆˆ T0), S1 âˆ© S2 âˆˆ T0 := by
    rintro S1 âŸ¨S1, hS1, rflâŸ© S2 âŸ¨S2, hS2, rflâŸ©
    exact âŸ¨S1 âˆ© S2, inter_mem hS1 hS2, by simp [basic_inter]âŸ©
  -- For every S âˆˆ T0, the intersection AA âˆ© S is nonempty.
  have claim4 : âˆ€ S âˆˆ T0, (AA âˆ© S).Nonempty := by
    rintro S âŸ¨S, hS, rflâŸ©
    rcases claim2 _ hS with âŸ¨G, hG, hG2âŸ©
    exact âŸ¨G, hG2, hGâŸ©
  -- Every element of T0 is nonempty.
  have claim5 : âˆ€ S âˆˆ T0, Set.Nonempty S := by
    rintro S âŸ¨S, hS, rflâŸ©
    exact âŸ¨F, hSâŸ©
  -- Every element of T2 is nonempty.
  have claim6 : âˆ€ S âˆˆ T2, Set.Nonempty S := by
    suffices âˆ€ S âˆˆ T2, S âˆˆ T0 âˆ¨ âˆƒ Q âˆˆ T0, S = AA âˆ© Q by
      intro S hS
      cases' this _ hS with h h
      Â· exact claim5 S h
      Â· rcases h with âŸ¨Q, hQ, rflâŸ©
        exact claim4 Q hQ
    intro S hS
    apply finiteInterClosure_insert
    Â· constructor
      Â· use Set.univ
        refine' âŸ¨Filter.univ_sets _, _âŸ©
        ext
        refine' âŸ¨_, by tautoâŸ©
        Â· intro
          apply Filter.univ_sets
      Â· exact claim3
    Â· exact hS
  -- It suffices to show that the intersection of any finite subset of T1 is nonempty.
  suffices âˆ€ F : fsu, â†‘F âŠ† T1 â†’ (â‹‚â‚€ Î¹ F).Nonempty by
    obtain âŸ¨G, h1âŸ© := Ultrafilter.exists_ultrafilter_of_finite_inter_nonempty _ this
    have c1 : X.join G = F := Ultrafilter.coe_le_coe.1 fun P hP => h1 (Or.inr âŸ¨P, hP, rflâŸ©)
    have c2 : G.map X.str = X.incl x := by
      refine' Ultrafilter.coe_le_coe.1 fun P hP => _
      apply mem_of_superset (h1 (Or.inl rfl))
      rintro x âŸ¨rflâŸ©
      exact hP
    simp [â† c1, c2]
  -- Finish...
  intro T hT
  -- âŠ¢ Set.Nonempty (â‹‚â‚€ Î¹ T)
  refine' claim6 _ (finiteInter_mem (.finiteInterClosure_finiteInter _) _ _)
  -- âŠ¢ â†‘T âŠ† finiteInterClosure T1
  intro t ht
  -- âŠ¢ t âˆˆ finiteInterClosure T1
  exact finiteInterClosure.basic (@hT t ht)
  -- ğŸ‰ no goals
#align Compactum.str_eq_of_le_nhds Compactum.str_eq_of_le_nhds

theorem le_nhds_of_str_eq {X : Compactum} (F : Ultrafilter X) (x : X) : X.str F = x â†’ â†‘F â‰¤ ğ“ x :=
  fun h => le_nhds_iff.mpr fun s hx hs => hs _ <| by rwa [h]
                                                     -- ğŸ‰ no goals
#align Compactum.le_nhds_of_str_eq Compactum.le_nhds_of_str_eq

-- All the hard work above boils down to this `T2Space` instance.
instance {X : Compactum} : T2Space X := by
  rw [t2_iff_ultrafilter]
  -- âŠ¢ âˆ€ {x y : X.A} (f : Ultrafilter X.A), â†‘f â‰¤ ğ“ x â†’ â†‘f â‰¤ ğ“ y â†’ x = y
  intro _ _ F hx hy
  -- âŠ¢ xâœ = yâœ
  rw [â† str_eq_of_le_nhds _ _ hx, â† str_eq_of_le_nhds _ _ hy]
  -- ğŸ‰ no goals

/-- The structure map of a compactum actually computes limits. -/
theorem lim_eq_str {X : Compactum} (F : Ultrafilter X) : F.lim = X.str F := by
  rw [Ultrafilter.lim_eq_iff_le_nhds, le_nhds_iff]
  -- âŠ¢ âˆ€ (s : Set X.A), str X F âˆˆ s â†’ IsOpen s â†’ s âˆˆ â†‘F
  tauto
  -- ğŸ‰ no goals
#align Compactum.Lim_eq_str Compactum.lim_eq_str

theorem cl_eq_closure {X : Compactum} (A : Set X) : cl A = closure A := by
  ext
  -- âŠ¢ xâœ âˆˆ Compactum.cl A â†” xâœ âˆˆ closure A
  rw [mem_closure_iff_ultrafilter]
  -- âŠ¢ xâœ âˆˆ Compactum.cl A â†” âˆƒ u, A âˆˆ u âˆ§ â†‘u â‰¤ ğ“ xâœ
  constructor
  -- âŠ¢ xâœ âˆˆ Compactum.cl A â†’ âˆƒ u, A âˆˆ u âˆ§ â†‘u â‰¤ ğ“ xâœ
  Â· rintro âŸ¨F, h1, h2âŸ©
    -- âŠ¢ âˆƒ u, A âˆˆ u âˆ§ â†‘u â‰¤ ğ“ xâœ
    exact âŸ¨F, h1, le_nhds_of_str_eq _ _ h2âŸ©
    -- ğŸ‰ no goals
  Â· rintro âŸ¨F, h1, h2âŸ©
    -- âŠ¢ xâœ âˆˆ Compactum.cl A
    exact âŸ¨F, h1, str_eq_of_le_nhds _ _ h2âŸ©
    -- ğŸ‰ no goals
#align Compactum.cl_eq_closure Compactum.cl_eq_closure

/-- Any morphism of compacta is continuous. -/
theorem continuous_of_hom {X Y : Compactum} (f : X âŸ¶ Y) : Continuous f := by
  rw [continuous_iff_ultrafilter]
  -- âŠ¢ âˆ€ (x : X.A) (g : Ultrafilter X.A), â†‘g â‰¤ ğ“ x â†’ Tendsto f.f (â†‘g) (ğ“ (Monad.Alg â€¦
  intro x g h
  -- âŠ¢ Tendsto f.f (â†‘g) (ğ“ (Monad.Algebra.Hom.f f x))
  rw [Tendsto, â† coe_map]
  -- âŠ¢ â†‘(Ultrafilter.map f.f g) â‰¤ ğ“ (Monad.Algebra.Hom.f f x)
  apply le_nhds_of_str_eq
  -- âŠ¢ str Y (Ultrafilter.map f.f g) = Monad.Algebra.Hom.f f x
  rw [â† str_hom_commute, str_eq_of_le_nhds _ x _]
  -- âŠ¢ â†‘g â‰¤ ğ“ x
  apply h
  -- ğŸ‰ no goals
#align Compactum.continuous_of_hom Compactum.continuous_of_hom

/-- Given any compact Hausdorff space, we construct a Compactum. -/
noncomputable def ofTopologicalSpace (X : Type*) [TopologicalSpace X] [CompactSpace X]
    [T2Space X] : Compactum where
  A := X
  a := Ultrafilter.lim
  unit := by
    ext x
    -- âŠ¢ (NatTrans.app (Monad.Î· Î²) X â‰« Ultrafilter.lim) x = ğŸ™ X x
    exact lim_eq (pure_le_nhds _)
    -- ğŸ‰ no goals
  assoc := by
    ext FF
    -- âŠ¢ (NatTrans.app (Monad.Î¼ Î²) X â‰« Ultrafilter.lim) FF = (Î².toFunctor.map Ultrafi â€¦
    change Ultrafilter (Ultrafilter X) at FF
    -- âŠ¢ (NatTrans.app (Monad.Î¼ Î²) X â‰« Ultrafilter.lim) FF = (Î².toFunctor.map Ultrafi â€¦
    set x := (Ultrafilter.map Ultrafilter.lim FF).lim with c1
    -- âŠ¢ (NatTrans.app (Monad.Î¼ Î²) X â‰« Ultrafilter.lim) FF = (Î².toFunctor.map Ultrafi â€¦
    have c2 : âˆ€ (U : Set X) (F : Ultrafilter X), F.lim âˆˆ U â†’ IsOpen U â†’ U âˆˆ F := by
      intro U F h1 hU
      exact isOpen_iff_ultrafilter.mp hU _ h1 _ (Ultrafilter.le_nhds_lim _)
    have c3 : â†‘(Ultrafilter.map Ultrafilter.lim FF) â‰¤ ğ“ x := by
      rw [le_nhds_iff]
      intro U hx hU
      exact mem_coe.2 (c2 _ _ (by rwa [â† c1]) hU)
    have c4 : âˆ€ U : Set X, x âˆˆ U â†’ IsOpen U â†’ { G : Ultrafilter X | U âˆˆ G } âˆˆ FF := by
      intro U hx hU
      suffices Ultrafilter.lim â»Â¹' U âˆˆ FF by
        apply mem_of_superset this
        intro P hP
        exact c2 U P hP hU
      exact @c3 U (IsOpen.mem_nhds hU hx)
    apply lim_eq
    -- âŠ¢ â†‘(NatTrans.app (Monad.Î¼ Î²) X FF) â‰¤ ğ“ ((Î².toFunctor.map Ultrafilter.lim â‰« Ult â€¦
    rw [le_nhds_iff]
    -- âŠ¢ âˆ€ (s : Set X), (Î².toFunctor.map Ultrafilter.lim â‰« Ultrafilter.lim) FF âˆˆ s â†’  â€¦
    exact c4
    -- ğŸ‰ no goals
#align Compactum.of_topological_space Compactum.ofTopologicalSpace

/-- Any continuous map between Compacta is a morphism of compacta. -/
def homOfContinuous {X Y : Compactum} (f : X â†’ Y) (cont : Continuous f) : X âŸ¶ Y :=
  { f
    h := by
      rw [continuous_iff_ultrafilter] at cont
      -- âŠ¢ Î².toFunctor.map f â‰« Y.a = X.a â‰« f
      ext (F : Ultrafilter X)
      -- âŠ¢ (Î².toFunctor.map f â‰« Y.a) F = (X.a â‰« f) F
      specialize cont (X.str F) F (le_nhds_of_str_eq F (X.str F) rfl)
      -- âŠ¢ (Î².toFunctor.map f â‰« Y.a) F = (X.a â‰« f) F
      simp only [types_comp_apply, ofTypeFunctor_map]
      -- âŠ¢ Monad.Algebra.a Y (Î².toFunctor.map f F) = f (Monad.Algebra.a X F)
      exact str_eq_of_le_nhds (Ultrafilter.map f F) _ cont }
      -- ğŸ‰ no goals
#align Compactum.hom_of_continuous Compactum.homOfContinuous

end Compactum

/-- The functor functor from Compactum to CompHaus. -/
def compactumToCompHaus : Compactum â¥¤ CompHaus where
  obj X := { toTop := { Î± := X } }
  map := @fun X Y f =>
    { toFun := f
      continuous_toFun := Compactum.continuous_of_hom _ }
#align Compactum_to_CompHaus compactumToCompHaus

namespace compactumToCompHaus

/-- The functor `compactumToCompHaus` is full. -/
def full : Full compactumToCompHaus.{u} where preimage X Y {f} := Compactum.homOfContinuous f.1 f.2
#align Compactum_to_CompHaus.full compactumToCompHaus.full

/-- The functor `compactumToCompHaus` is faithful. -/
theorem faithful : Faithful compactumToCompHaus where
  -- Porting note: this used to be obviously (though it consumed a bit of memory)
  map_injective := by
    intro _ _ _ _ h
    -- âŠ¢ aâ‚âœ = aâ‚‚âœ
    -- Porting note: ext gets confused by coercion using forget
    apply Monad.Algebra.Hom.ext
    -- âŠ¢ aâ‚âœ.f = aâ‚‚âœ.f
    apply congrArg (fun f => f.toFun) h
    -- ğŸ‰ no goals
#align Compactum_to_CompHaus.faithful compactumToCompHaus.faithful

/-- This definition is used to prove essential surjectivity of `compactumToCompHaus`. -/
def isoOfTopologicalSpace {D : CompHaus} :
    compactumToCompHaus.obj (Compactum.ofTopologicalSpace D) â‰… D where
  hom :=
    { toFun := id
      continuous_toFun :=
        continuous_def.2 fun _ h => by
          rw [isOpen_iff_ultrafilter'] at h
          -- âŠ¢ IsOpen (id â»Â¹' xâœ)
          exact h }
          -- ğŸ‰ no goals
  inv :=
    { toFun := id
      continuous_toFun :=
        continuous_def.2 fun _ h1 => by
          rw [isOpen_iff_ultrafilter']
          -- âŠ¢ âˆ€ (F : Ultrafilter â†‘D.toTop), Ultrafilter.lim F âˆˆ id â»Â¹' xâœ â†’ id â»Â¹' xâœ âˆˆ â†‘F
          intro _ h2
          -- âŠ¢ id â»Â¹' xâœ âˆˆ â†‘Fâœ
          exact h1 _ h2 }
          -- ğŸ‰ no goals
#align Compactum_to_CompHaus.iso_of_topological_space compactumToCompHaus.isoOfTopologicalSpace

/-- The functor `compactumToCompHaus` is essentially surjective. -/
theorem essSurj : EssSurj compactumToCompHaus :=
  { mem_essImage := fun X => âŸ¨Compactum.ofTopologicalSpace X, âŸ¨isoOfTopologicalSpaceâŸ©âŸ© }
#align Compactum_to_CompHaus.ess_surj compactumToCompHaus.essSurj

/-- The functor `compactumToCompHaus` is an equivalence of categories. -/
noncomputable instance isEquivalence : IsEquivalence compactumToCompHaus := by
  have := compactumToCompHaus.full
  -- âŠ¢ IsEquivalence compactumToCompHaus
  have := compactumToCompHaus.faithful
  -- âŠ¢ IsEquivalence compactumToCompHaus
  have := compactumToCompHaus.essSurj
  -- âŠ¢ IsEquivalence compactumToCompHaus
  apply Equivalence.ofFullyFaithfullyEssSurj _
  -- ğŸ‰ no goals
#align Compactum_to_CompHaus.is_equivalence compactumToCompHaus.isEquivalence

end compactumToCompHaus

/-- The forgetful functors of `Compactum` and `CompHaus` are compatible via
`compactumToCompHaus`. -/
def compactumToCompHausCompForget :
    compactumToCompHaus â‹™ CategoryTheory.forget CompHaus â‰… Compactum.forget :=
  NatIso.ofComponents fun X => eqToIso rfl
#align Compactum_to_CompHaus_comp_forget compactumToCompHausCompForget

/-
TODO: `forget CompHaus` is monadic, as it is isomorphic to the composition
of an equivalence with the monadic functor `forget Compactum`.
Once we have the API to transfer monadicity of functors along such isomorphisms,
the instance `CreatesLimits (forget CompHaus)` can be deduced from this
monadicity.
-/
noncomputable instance CompHaus.forgetCreatesLimits : CreatesLimits (forget CompHaus) := by
  let e : forget CompHaus â‰… compactumToCompHaus.inv â‹™ Compactum.forget :=
    (((forget CompHaus).leftUnitor.symm â‰ªâ‰«
    isoWhiskerRight compactumToCompHaus.asEquivalence.symm.unitIso (forget CompHaus)) â‰ªâ‰«
    compactumToCompHaus.inv.associator compactumToCompHaus (forget CompHaus)) â‰ªâ‰«
    isoWhiskerLeft _ compactumToCompHausCompForget
  exact createsLimitsOfNatIso e.symm
  -- ğŸ‰ no goals

#align CompHaus.forget_creates_limits CompHaus.forgetCreatesLimits

noncomputable instance Profinite.forgetCreatesLimits : CreatesLimits (forget Profinite) := by
  change CreatesLimits (profiniteToCompHaus â‹™ forget _)
  -- âŠ¢ CreatesLimits (profiniteToCompHaus â‹™ forget CompHaus)
  infer_instance
  -- ğŸ‰ no goals
#align Profinite.forget_creates_limits Profinite.forgetCreatesLimits
