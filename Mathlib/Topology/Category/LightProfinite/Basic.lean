/-
Copyright (c) 2023 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson
-/
import Mathlib.CategoryTheory.Limits.Shapes.Countable
import Mathlib.Topology.Category.Profinite.AsLimit
import Mathlib.Topology.Category.Profinite.CofilteredLimit
import Mathlib.Topology.ClopenBox
/-!

# Light profinite spaces

We construct the category `LightProfinite` of light profinite topological spaces. These are
implemented as totally disconnected second countable compact Hausdorff spaces.

This file also defines the category `LightDiagram`, which consists of those spaces that can be
written as a sequential limit (in `Profinite`) of finite sets.

We define an equivalence of categories `LightProfinite â‰Œ LightDiagram` and prove that these are
essentially small categories.

## Implementation

The category `LightProfinite` is defined using the structure `CompHausLike`. See the file
`CompHausLike.Basic` for more information.

-/

/- The basic API for `LightProfinite` is largely copied from the API of `Profinite`;
where possible, try to keep them in sync -/

universe v u

open CategoryTheory Limits Opposite FintypeCat Topology TopologicalSpace CompHausLike

/-- `LightProfinite` is the category of second countable profinite spaces. -/
abbrev LightProfinite := CompHausLike
  (fun X â†¦ TotallyDisconnectedSpace X âˆ§ SecondCountableTopology X)

namespace LightProfinite

instance (X : Type*) [TopologicalSpace X]
    [TotallyDisconnectedSpace X] [SecondCountableTopology X] : HasProp (fun Y â†¦
      TotallyDisconnectedSpace Y âˆ§ SecondCountableTopology Y) X :=
  âŸ¨âŸ¨(inferInstance : TotallyDisconnectedSpace X), (inferInstance : SecondCountableTopology X)âŸ©âŸ©

/--
Construct a term of `LightProfinite` from a type endowed with the structure of a compact,
Hausdorff, totally disconnected and second countable topological space.
-/
abbrev of (X : Type*) [TopologicalSpace X] [CompactSpace X] [T2Space X]
    [TotallyDisconnectedSpace X] [SecondCountableTopology X] : LightProfinite :=
  CompHausLike.of _ X

instance : Inhabited LightProfinite :=
  âŸ¨LightProfinite.of PEmptyâŸ©

instance {X : LightProfinite} : TotallyDisconnectedSpace X :=
  X.prop.1

instance {X : LightProfinite} : SecondCountableTopology X :=
  X.prop.2

end LightProfinite

/-- The fully faithful embedding of `LightProfinite` in `Profinite`. -/
abbrev lightToProfinite : LightProfinite â¥¤ Profinite :=
  CompHausLike.toCompHausLike (fun _ â†¦ inferInstance)

/-- `lightToProfinite` is fully faithful. -/
abbrev lightToProfiniteFullyFaithful : lightToProfinite.FullyFaithful :=
  fullyFaithfulToCompHausLike _

/-- The fully faithful embedding of `LightProfinite` in `CompHaus`. -/
abbrev lightProfiniteToCompHaus : LightProfinite â¥¤ CompHaus :=
  compHausLikeToCompHaus _

/-- The fully faithful embedding of `LightProfinite` in `TopCat`.
This is definitionally the same as the obvious composite. -/
abbrev LightProfinite.toTopCat : LightProfinite â¥¤ TopCat :=
  CompHausLike.compHausLikeToTop _

section DiscreteTopology

attribute [local instance] FintypeCat.botTopology
attribute [local instance] FintypeCat.discreteTopology

/-- The natural functor from `Fintype` to `LightProfinite`, endowing a finite type with the
discrete topology. -/
@[simps! -isSimp map_hom_apply]
def FintypeCat.toLightProfinite : FintypeCat â¥¤ LightProfinite where
  obj A := LightProfinite.of A
  map f := CompHausLike.ofHom _ âŸ¨f, by continuityâŸ©

/-- `FintypeCat.toLightProfinite` is fully faithful. -/
def FintypeCat.toLightProfiniteFullyFaithful : toLightProfinite.FullyFaithful where
  preimage f := (f : _ â†’ _)
  map_preimage _ := rfl
  preimage_map _ := rfl

instance : FintypeCat.toLightProfinite.Faithful :=
  FintypeCat.toLightProfiniteFullyFaithful.faithful

instance : FintypeCat.toLightProfinite.Full :=
  FintypeCat.toLightProfiniteFullyFaithful.full

instance (X : FintypeCat.{u}) : Fintype (FintypeCat.toLightProfinite.obj X) :=
  inferInstanceAs (Fintype X)

instance (X : FintypeCat.{u}) : Fintype (LightProfinite.of X) :=  inferInstanceAs (Fintype X)

end DiscreteTopology

namespace LightProfinite

instance {J : Type v} [SmallCategory J] (F : J â¥¤ LightProfinite.{max u v}) :
    TotallyDisconnectedSpace
      (CompHaus.limitCone.{v, u} (F â‹™ lightProfiniteToCompHaus)).pt.toTop := by
  change TotallyDisconnectedSpace ({ u : âˆ€ j : J, F.obj j | _ } : Type _)
  exact Subtype.totallyDisconnectedSpace

/-- An explicit limit cone for a functor `F : J â¥¤ LightProfinite`, for a countable category `J`
  defined in terms of `CompHaus.limitCone`, which is defined in terms of `TopCat.limitCone`. -/
def limitCone {J : Type v} [SmallCategory J] [CountableCategory J]
    (F : J â¥¤ LightProfinite.{max u v}) :
    Limits.Cone F where
  pt :=
    { toTop := (CompHaus.limitCone.{v, u} (F â‹™ lightProfiniteToCompHaus)).pt.toTop
      prop := by
        constructor
        Â· infer_instance
        Â· change SecondCountableTopology ({ u : âˆ€ j : J, F.obj j | _ } : Type _)
          apply IsInducing.subtypeVal.secondCountableTopology }
  Ï€ :=
  { app := (CompHaus.limitCone.{v, u} (F â‹™ lightProfiniteToCompHaus)).Ï€.app
    naturality := by
      intro j k f
      ext âŸ¨g, pâŸ©
      exact (p f).symm }

/-- The limit cone `LightProfinite.limitCone F` is indeed a limit cone. -/
def limitConeIsLimit {J : Type v} [SmallCategory J] [CountableCategory J]
    (F : J â¥¤ LightProfinite.{max u v}) :
    Limits.IsLimit (limitCone F) where
  lift S :=
    (CompHaus.limitConeIsLimit.{v, u} (F â‹™ lightProfiniteToCompHaus)).lift
      (lightProfiniteToCompHaus.mapCone S)
  uniq S _ h := (CompHaus.limitConeIsLimit.{v, u} _).uniq (lightProfiniteToCompHaus.mapCone S) _ h

noncomputable instance createsCountableLimits {J : Type v} [SmallCategory J] [CountableCategory J] :
    CreatesLimitsOfShape J lightToProfinite.{max v u} where
  CreatesLimit {F} :=
    createsLimitOfFullyFaithfulOfIso (limitCone.{v, u} F).pt <|
      (Profinite.limitConeIsLimit.{v, u} (F â‹™ lightToProfinite)).conePointUniqueUpToIso
        (limit.isLimit _)

instance : HasCountableLimits LightProfinite where
  out _ := { has_limit := fun F â†¦ âŸ¨limitCone F, limitConeIsLimit FâŸ© }

instance : PreservesLimitsOfShape â„•áµ’áµ– (forget LightProfinite.{u}) :=
  have : PreservesLimitsOfSize.{0, 0} (forget Profinite.{u}) := preservesLimitsOfSize_shrink _
  inferInstanceAs (PreservesLimitsOfShape â„•áµ’áµ– (lightToProfinite â‹™ forget Profinite))

variable {X Y : LightProfinite.{u}} (f : X âŸ¶ Y)

/-- Any morphism of light profinite spaces is a closed map. -/
theorem isClosedMap : IsClosedMap f :=
  CompHausLike.isClosedMap _

/-- Any continuous bijection of light profinite spaces induces an isomorphism. -/
theorem isIso_of_bijective (bij : Function.Bijective f) : IsIso f :=
  haveI := CompHausLike.isIso_of_bijective (lightProfiniteToCompHaus.map f) bij
  isIso_of_fully_faithful lightProfiniteToCompHaus _

/-- Any continuous bijection of light profinite spaces induces an isomorphism. -/
noncomputable def isoOfBijective (bij : Function.Bijective f) : X â‰… Y :=
  letI := LightProfinite.isIso_of_bijective f bij
  asIso f

instance forget_reflectsIsomorphisms : (forget LightProfinite).ReflectsIsomorphisms := by
  constructor
  intro A B f hf
  rw [isIso_iff_bijective] at hf
  exact LightProfinite.isIso_of_bijective _ hf

theorem epi_iff_surjective {X Y : LightProfinite.{u}} (f : X âŸ¶ Y) :
    Epi f â†” Function.Surjective f := by
  constructor
  Â· -- Note: in mathlib3 `contrapose` saw through `Function.Surjective`.
    dsimp [Function.Surjective]
    contrapose!
    rintro âŸ¨y, hyâŸ© hf
    let C := Set.range f
    have hC : IsClosed C := (isCompact_range f.hom.continuous).isClosed
    let U := Cá¶œ
    have hyU : y âˆˆ U := by
      refine Set.mem_compl ?_
      rintro âŸ¨y', hy'âŸ©
      exact hy y' hy'
    have hUy : U âˆˆ ð“ y := hC.compl_mem_nhds hyU
    obtain âŸ¨V, hV, hyV, hVUâŸ© := isTopologicalBasis_isClopen.mem_nhds_iff.mp hUy
    classical
      let Z := of (ULift.{u} <| Fin 2)
      let g : Y âŸ¶ Z := CompHausLike.ofHom _
        âŸ¨(LocallyConstant.ofIsClopen hV).map ULift.up, LocallyConstant.continuous _âŸ©
      let h : Y âŸ¶ Z := CompHausLike.ofHom _ âŸ¨fun _ â†¦ âŸ¨1âŸ©, continuous_constâŸ©
      have H : h = g := by
        rw [â† cancel_epi f]
        ext x
        dsimp [g, LocallyConstant.ofIsClopen]
        rw [ContinuousMap.coe_mk, ContinuousMap.coe_mk, hom_ofHom, ContinuousMap.coe_mk,
          Function.comp_apply, if_neg]
        refine mt (fun Î± â†¦ hVU Î±) ?_
        simp [U, C]
      apply_fun fun e â†¦ (e y).down at H
      dsimp [g, LocallyConstant.ofIsClopen] at H
      rw [ContinuousMap.coe_mk, ContinuousMap.coe_mk, Function.comp_apply, if_pos hyV] at H
      exact top_ne_bot H
  Â· rw [â† CategoryTheory.epi_iff_surjective]
    apply (forget LightProfinite).epi_of_epi_map

instance : lightToProfinite.PreservesEpimorphisms where
  preserves f _ := (Profinite.epi_iff_surjective _).mpr ((epi_iff_surjective f).mp inferInstance)

end LightProfinite

/-- A structure containing the data of sequential limit in `Profinite` of finite sets. -/
structure LightDiagram : Type (u+1) where
  /-- The indexing diagram. -/
  diagram : â„•áµ’áµ– â¥¤ FintypeCat
  /-- The limit cone. -/
  cone : Cone (diagram â‹™ FintypeCat.toProfinite.{u})
  /-- The limit cone is limiting. -/
  isLimit : IsLimit cone

namespace LightDiagram

/-- The underlying `Profinite` of a `LightDiagram`. -/
def toProfinite (S : LightDiagram) : Profinite := S.cone.pt

@[simps!]
instance : Category LightDiagram := InducedCategory.category toProfinite

instance hasForget : ConcreteCategory LightDiagram (fun X Y â†¦ C(X.toProfinite, Y.toProfinite)) :=
  InducedCategory.concreteCategory toProfinite

end LightDiagram

/-- The fully faithful embedding `LightDiagram â¥¤ Profinite` -/
@[simps!]
def lightDiagramToProfinite : LightDiagram â¥¤ Profinite := inducedFunctor _

instance : lightDiagramToProfinite.Faithful := show (inducedFunctor _).Faithful from inferInstance

instance : lightDiagramToProfinite.Full := show (inducedFunctor _).Full from inferInstance

namespace LightProfinite

instance (S : LightProfinite) : Countable (Clopens S) := by
  rw [TopologicalSpace.Clopens.countable_iff_secondCountable]
  infer_instance

instance instCountableDiscreteQuotient (S : LightProfinite) :
    Countable (DiscreteQuotient ((lightToProfinite.obj S))) :=
  (DiscreteQuotient.finsetClopens_inj S).countable

/-- A profinite space which is light gives rise to a light profinite space. -/
noncomputable def toLightDiagram (S : LightProfinite.{u}) : LightDiagram.{u} where
  diagram := IsCofiltered.sequentialFunctor _ â‹™ (lightToProfinite.obj S).fintypeDiagram
  cone := (Functor.Initial.limitConeComp (IsCofiltered.sequentialFunctor _)
    (lightToProfinite.obj S).lim).cone
  isLimit := (Functor.Initial.limitConeComp (IsCofiltered.sequentialFunctor _)
    (lightToProfinite.obj S).lim).isLimit

end LightProfinite

/-- The functor part of the equivalence `LightProfinite â‰Œ LightDiagram` -/
@[simps]
noncomputable def lightProfiniteToLightDiagram : LightProfinite.{u} â¥¤ LightDiagram.{u} where
  obj X := X.toLightDiagram
  map f := f

open scoped Classical in
instance (S : LightDiagram.{u}) : SecondCountableTopology S.cone.pt := by
  rw [â† TopologicalSpace.Clopens.countable_iff_secondCountable]
  refine @Countable.of_equiv _ _ ?_ (LocallyConstant.equivClopens (X := S.cone.pt))
  refine @Function.Surjective.countable
    (Î£ (n : â„•), LocallyConstant ((S.diagram â‹™ FintypeCat.toProfinite).obj âŸ¨nâŸ©) (Fin 2)) _ ?_ ?_ ?_
  Â· apply @instCountableSigma _ _ _ ?_
    intro n
    refine @Finite.to_countable _ ?_
    refine @Finite.of_injective _ ((S.diagram â‹™ FintypeCat.toProfinite).obj âŸ¨nâŸ© â†’ (Fin 2)) ?_ _
      LocallyConstant.coe_injective
    refine @Pi.finite _ _ ?_ _
    simp only [Functor.comp_obj]
    exact show (Finite (S.diagram.obj _)) from inferInstance
  Â· exact fun a â†¦ a.snd.comap (S.cone.Ï€.app âŸ¨a.fstâŸ©).hom
  Â· intro a
    obtain âŸ¨n, g, hâŸ© := Profinite.exists_locallyConstant S.cone S.isLimit a
    exact âŸ¨âŸ¨unop n, gâŸ©, h.symmâŸ©

/-- The inverse part of the equivalence `LightProfinite â‰Œ LightDiagram` -/
@[simps obj map]
def lightDiagramToLightProfinite : LightDiagram.{u} â¥¤ LightProfinite.{u} where
  obj X := LightProfinite.of X.cone.pt
  map f := f

/-- The equivalence of categories `LightProfinite â‰Œ LightDiagram` -/
noncomputable def LightProfinite.equivDiagram : LightProfinite.{u} â‰Œ LightDiagram.{u} where
  functor := lightProfiniteToLightDiagram
  inverse := lightDiagramToLightProfinite
  unitIso := Iso.refl _
  counitIso := NatIso.ofComponents
    (fun _ â†¦ lightDiagramToProfinite.preimageIso (Iso.refl _)) (by
      intro _ _ f
      simp only [Functor.comp_obj, lightDiagramToLightProfinite_obj,
        lightProfiniteToLightDiagram_obj, Functor.id_obj, Functor.comp_map,
        lightDiagramToLightProfinite_map, lightProfiniteToLightDiagram_map,
        lightDiagramToProfinite_obj, Functor.preimageIso_hom, Iso.refl_hom, Functor.id_map]
      erw [lightDiagramToProfinite.preimage_id, lightDiagramToProfinite.preimage_id,
        Category.comp_id f])
  functor_unitIso_comp _ := by simpa using lightDiagramToProfinite.preimage_id

instance : lightProfiniteToLightDiagram.IsEquivalence :=
  show LightProfinite.equivDiagram.functor.IsEquivalence from inferInstance

instance : lightDiagramToLightProfinite.IsEquivalence :=
  show LightProfinite.equivDiagram.inverse.IsEquivalence from inferInstance

noncomputable section EssentiallySmall

open LightDiagram

/--
This is an auxiliary definition used to show that `LightDiagram` is essentially small.

Note that below we put a category instance on this structure which is completely different from the
category instance on `â„•áµ’áµ– â¥¤ FintypeCat.Skeleton.{u}`. Neither the morphisms nor the objects are the
same.
-/
structure LightDiagram' : Type u where
  /-- The diagram takes values in a small category equivalent to `FintypeCat`. -/
  diagram : â„•áµ’áµ– â¥¤ FintypeCat.Skeleton.{u}

/-- A `LightDiagram'` yields a `Profinite`. -/
def LightDiagram'.toProfinite (S : LightDiagram') : Profinite :=
  limit (S.diagram  â‹™ FintypeCat.Skeleton.equivalence.functor â‹™ FintypeCat.toProfinite.{u})

instance : Category LightDiagram' := InducedCategory.category LightDiagram'.toProfinite

/-- The functor part of the equivalence of categories `LightDiagram' â‰Œ LightDiagram`. -/
def LightDiagram'.toLightFunctor : LightDiagram'.{u} â¥¤ LightDiagram.{u} where
  obj X := âŸ¨X.diagram â‹™ Skeleton.equivalence.functor, _, limit.isLimit _âŸ©
  map f := f

instance : LightDiagram'.toLightFunctor.{u}.Faithful := âŸ¨idâŸ©

instance : LightDiagram'.toLightFunctor.{u}.Full where
  map_surjective f := âŸ¨f, rflâŸ©

instance : LightDiagram'.toLightFunctor.{u}.EssSurj where
  mem_essImage Y :=
    âŸ¨âŸ¨Y.diagram â‹™ Skeleton.equivalence.inverseâŸ©, âŸ¨lightDiagramToProfinite.preimageIso (
      (Limits.lim.mapIso (Functor.isoWhiskerRight ((Functor.isoWhiskerLeft Y.diagram
      Skeleton.equivalence.counitIso)) toProfinite)) â‰ªâ‰«
      (limit.isLimit _).conePointUniqueUpToIso Y.isLimit)âŸ©âŸ©

instance : LightDiagram'.toLightFunctor.IsEquivalence where

/-- The equivalence between `LightDiagram` and a small category. -/
def LightDiagram.equivSmall : LightDiagram.{u} â‰Œ LightDiagram'.{u} :=
  LightDiagram'.toLightFunctor.asEquivalence.symm

instance : EssentiallySmall.{u} LightDiagram.{u} where
  equiv_smallCategory := âŸ¨LightDiagram', inferInstance, âŸ¨LightDiagram.equivSmallâŸ©âŸ©

instance : EssentiallySmall.{u} LightProfinite.{u} where
  equiv_smallCategory := âŸ¨LightDiagram', inferInstance,
    âŸ¨LightProfinite.equivDiagram.trans LightDiagram.equivSmallâŸ©âŸ©

end EssentiallySmall
