/-
Copyright (c) 2023 Jon Eugster. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson, Boris Bolvig KjÃ¦r, Jon Eugster, Sina Hazratpour, Nima Rasekh
-/
import Mathlib.Topology.Category.Stonean.Limits
import Mathlib.Topology.Category.CompHaus.EffectiveEpi

/-!
# Effective epimorphic families in `Stonean`

Let `Ï€ a : X a âŸ¶ B` be a family of morphisms in `Stonean` indexed by a finite type `Î±`.
In this file, we show that the following are all equivalent:
- The family `Ï€` is effective epimorphic.
- The induced map `âˆ X âŸ¶ B` is epimorphic.
- The family `Ï€` is jointly surjective.

As a consequence, we show (see `effectiveEpi_iff_surjective`) that all epimorphisms in `Stonean`Â 
are effective, and that `Stonean`Â is preregular.

## Main results
- `Stonean.effectiveEpiFamily_tfae`: characterise being an effective epimorphic family.
- `Stonean.instPrecoherent`: `Stonean` is precoherent.

## Implementation notes
The entire section `EffectiveEpiFamily` comprises exclusively a technical construction for
the main proof and does not contain any statements that would be useful in other contexts.

-/

universe u

open CategoryTheory Limits

namespace Stonean

/- Assume we have a family `X a â†’ B` which is jointly surjective. -/
variable {Î± : Type} [Fintype Î±] {B : Stonean}
  {X : Î± â†’ Stonean} (Ï€ : (a : Î±) â†’ (X a âŸ¶ B))
  (surj : âˆ€ b : B, âˆƒ (a : Î±) (x : X a), Ï€ a x = b)

/-!
This section contains exclusively technical definitions and results that are used
in the proof of `Stonean.effectiveEpiFamily_of_jointly_surjective`.
-/
namespace EffectiveEpiFamily

/-- Implementation: Abbreviation for the fully faithful functor `Stonean â¥¤ CompHaus`. -/
abbrev F := Stonean.toCompHaus

open CompHaus in
/-- Implementation: A helper lemma lifting the condition

```
âˆ€ {Z : Stonean} (aâ‚ aâ‚‚ : Î±) (gâ‚ : Z âŸ¶ X aâ‚) (gâ‚‚ : Z âŸ¶ X aâ‚‚),
  gâ‚ â‰« Ï€ aâ‚ = gâ‚‚ â‰« Ï€ aâ‚‚ â†’ gâ‚ â‰« e aâ‚ = gâ‚‚ â‰« e aâ‚‚)
```

from `Z : Stonean` to `Z : CompHaus`.

The descent `EffectiveEpiFamily.dec` along an effective epi family in a category `C`
takes this condition (for all `Z` in `C`) as an assumption.

In the construction in this file we start with this descent condition for all `Z : Stonean` but
to apply the analogue result on `CompHaus` we need extend this condition to all
`Z : CompHaus`. We do this by considering the Stone-Cech compactification `Î²Z â†’ Z`
which is an epi in `CompHaus` covering `Z` where `Î²Z` lies in the image of `Stonean`.
-/
lemma lift_desc_condition {W : Stonean} {e : (a : Î±) â†’ X a âŸ¶ W}
    (h : âˆ€ {Z : Stonean} (aâ‚ aâ‚‚ : Î±) (gâ‚ : Z âŸ¶ X aâ‚) (gâ‚‚ : Z âŸ¶ X aâ‚‚),
      gâ‚ â‰« Ï€ aâ‚ = gâ‚‚ â‰« Ï€ aâ‚‚ â†’ gâ‚ â‰« e aâ‚ = gâ‚‚ â‰« e aâ‚‚) :
    âˆ€ {Z : CompHaus} (aâ‚ aâ‚‚ : Î±) (gâ‚ : Z âŸ¶ F.obj (X aâ‚)) (gâ‚‚ : Z âŸ¶ F.obj (X aâ‚‚)),
        gâ‚ â‰« (Ï€ aâ‚) = gâ‚‚ â‰« (Ï€ aâ‚‚) â†’ gâ‚ â‰« e aâ‚ = gâ‚‚ â‰« e aâ‚‚ := by
  intro Z aâ‚ aâ‚‚ gâ‚ gâ‚‚ hg
  -- The Stone-Cech-compactification `Î²Z` of `Z : CompHaus` is in `Stonean`
  let Î²Z := Z.presentation
  let gâ‚' := F.preimage (presentation.Ï€ Z â‰« gâ‚ : F.obj Î²Z âŸ¶ F.obj (X aâ‚))
  let gâ‚‚' := F.preimage (presentation.Ï€ Z â‰« gâ‚‚ : F.obj Î²Z âŸ¶ F.obj (X aâ‚‚))
  -- Use that `Î²Z â†’ Z` is an epi
  apply Epi.left_cancellation (f := presentation.Ï€ Z)
  -- By definition `gâ‚' = presentationÏ€ â‰« gâ‚` and `gâ‚‚' = presentationÏ€ â‰« gâ‚‚`
  change gâ‚' â‰« e aâ‚ = gâ‚‚' â‰« e aâ‚‚
  -- use the condition in `Stonean`
  apply h
  change presentation.Ï€ Z â‰« gâ‚ â‰« Ï€ aâ‚ = presentation.Ï€ Z â‰« gâ‚‚ â‰« Ï€ aâ‚‚
  simp [hg]

/-- Implementation: The structure for the `EffectiveEpiFamily X Ï€`. -/
noncomputable
def struct : EffectiveEpiFamilyStruct X Ï€ where
  desc := fun {W} e h => Stonean.toCompHaus.preimage <|
    -- Use the `EffectiveEpiFamily F(X) F(Ï€)` on `CompHaus`
    (CompHaus.effectiveEpiFamily_of_jointly_surjective (F.obj <| X Â·) Ï€ surj).desc
    (fun (a : Î±) => F.map (e a)) (lift_desc_condition Ï€ h)
  fac := by
    -- The `EffectiveEpiFamily F(X) F(Ï€)` on `CompHaus`
    let fam : EffectiveEpiFamily (F.obj <| X Â·) Ï€ :=
      CompHaus.effectiveEpiFamily_of_jointly_surjective (F.obj <| X Â·) Ï€ surj
    intro W e he a
    -- The `fac` on `CompHaus`
    have facâ‚ :  F.map (Ï€ a â‰« _) = F.map (e a) :=
      EffectiveEpiFamily.fac (F.obj <| X Â·) Ï€ e (lift_desc_condition Ï€ he) a
    exact Faithful.map_injective facâ‚
  uniq := by
    -- The `EffectiveEpiFamily F(X) F(Ï€)` on `CompHaus`
    let fam : EffectiveEpiFamily (F.obj <| X Â·) Ï€ :=
      CompHaus.effectiveEpiFamily_of_jointly_surjective (F.obj <| X Â·) Ï€ surj
    intro W e he m hm
    have Fhm : âˆ€ (a : Î±), Ï€ a â‰« F.map m = e a
    Â· intro a
      simp_all only [toCompHaus_map]
    have uniqâ‚ : F.map m = F.map _ :=
      EffectiveEpiFamily.uniq (F.obj <| X Â·) Ï€ e (lift_desc_condition Ï€ he) (F.map m) Fhm
    exact Faithful.map_injective uniqâ‚

end EffectiveEpiFamily

section JointlySurjective

/-- One direction of `effectiveEpiFamily_tfae`. -/
theorem effectiveEpiFamily_of_jointly_surjective
    {Î± : Type} [Fintype Î±] {B : Stonean}
    (X : Î± â†’ Stonean) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B))
    (surj : âˆ€ b : B, âˆƒ (a : Î±) (x : X a), Ï€ a x = b) :
    EffectiveEpiFamily X Ï€ :=
  âŸ¨âŸ¨Stonean.EffectiveEpiFamily.struct Ï€ surjâŸ©âŸ©

open List in
/--
For a finite family of extremally spaces `Ï€ a : X a â†’ B` the following are equivalent:
* `Ï€` is an effective epimorphic family
* the map `âˆ Ï€ a âŸ¶ B` is an epimorphism
* `Ï€` is jointly surjective
-/
theorem effectiveEpiFamily_tfae {Î± : Type} [Fintype Î±] {B : Stonean}
    (X : Î± â†’ Stonean) (Ï€ : (a : Î±) â†’ (X a âŸ¶ B)) :
    TFAE [
      EffectiveEpiFamily X Ï€,
      Epi (Limits.Sigma.desc Ï€),
      âˆ€ (b : B), âˆƒ (a : Î±) (x : X a), Ï€ a x = b ] := by
  tfae_have 1 â†’ 2
  Â· intro
    infer_instance
  tfae_have 1 â†’ 2
  Â· intro
    infer_instance
  tfae_have 2 â†’ 3
  Â· intro e
    rw [epi_iff_surjective] at e
    intro b
    obtain âŸ¨t, rflâŸ© := e b
    let q := (coproductIsoCoproduct X).inv t
    refine âŸ¨q.1, q.2, ?_âŸ©
    rw [â† (coproductIsoCoproduct X).inv_hom_id_apply t]
    show _ = ((coproductIsoCoproduct X).hom â‰« Sigma.desc Ï€) ((coproductIsoCoproduct X).inv t)
    suffices : (coproductIsoCoproduct X).hom â‰« Sigma.desc Ï€ = finiteCoproduct.desc X Ï€
    Â· rw [this]
      rfl
    apply Eq.symm
    rw [â† Iso.inv_comp_eq]
    apply colimit.hom_ext
    rintro âŸ¨aâŸ©
    simp only [Discrete.functor_obj, colimit.Î¹_desc, Cofan.mk_pt, Cofan.mk_Î¹_app,
      coproductIsoCoproduct, colimit.comp_coconePointUniqueUpToIso_inv_assoc]
    ext
    rfl
  tfae_have 3 â†’ 1
  Â· apply effectiveEpiFamily_of_jointly_surjective
  tfae_finish

lemma effectiveEpi_iff_surjective {X Y : Stonean} (f : X âŸ¶ Y) :
    EffectiveEpi f â†” Function.Surjective f := by
  rw [â† epi_iff_surjective]
  exact effectiveEpi_iff_epi (fun _ _ â†¦ (effectiveEpiFamily_tfae _ _).out 0 1) f

instance : Preregular Stonean where
  exists_fac := by
    intro X Y Z f Ï€ hÏ€
    exact âŸ¨X, ğŸ™ X, inferInstance, Projective.factors f Ï€âŸ©

end JointlySurjective

section Coherent

open CompHaus Functor

theorem _root_.CategoryTheory.EffectiveEpiFamily.toCompHaus
    {Î± : Type} [Fintype Î±] {B : Stonean.{u}}
    {X : Î± â†’ Stonean.{u}} {Ï€ : (a : Î±) â†’ (X a âŸ¶ B)} (H : EffectiveEpiFamily X Ï€) :
    EffectiveEpiFamily (toCompHaus.obj <| X Â·) (toCompHaus.map <| Ï€ Â·) := by
  refine' ((CompHaus.effectiveEpiFamily_tfae _ _).out 0 2).2 (fun b => _)
  exact (((effectiveEpiFamily_tfae _ _).out 0 2).1 H : âˆ€ _, âˆƒ _, _) _

instance instPrecoherent : Precoherent Stonean.{u} := by
  constructor
  intro Bâ‚ Bâ‚‚ f Î± _ Xâ‚ Ï€â‚ hâ‚
  refine âŸ¨Î±, inferInstance, fun a => (CompHaus.pullback f (Ï€â‚ a)).presentation, fun a =>
    toCompHaus.preimage (presentation.Ï€ _ â‰« (CompHaus.pullback.fst _ _)), ?_, id, fun a =>
    toCompHaus.preimage (presentation.Ï€ _ â‰« (CompHaus.pullback.snd _ _ )), fun a => ?_âŸ©
  Â· refine ((effectiveEpiFamily_tfae _ _).out 0 2).2 (fun b => ?_)
    have hâ‚' := ((CompHaus.effectiveEpiFamily_tfae _ _).out 0 2).1 hâ‚.toCompHaus
    obtain âŸ¨a, x, hâŸ© := hâ‚' (f b)
    obtain âŸ¨c, hcâŸ© := (CompHaus.epi_iff_surjective _).1
      (presentation.epi_Ï€ (CompHaus.pullback f (Ï€â‚ a))) âŸ¨âŸ¨b, xâŸ©, h.symmâŸ©
    refine âŸ¨a, c, ?_âŸ©
    change toCompHaus.map (toCompHaus.preimage _) _ = _
    simp only [image_preimage, toCompHaus_obj, comp_apply, hc]
    rfl
  Â· apply map_injective toCompHaus
    simp only [map_comp, image_preimage, Category.assoc]
    congr 1
    ext âŸ¨âŸ¨_, _âŸ©, hâŸ©
    exact h.symm

end Coherent

instance : Preregular Stonean where
  exists_fac := by
    intro X Y Z f Ï€ hÏ€
    exact âŸ¨X, ğŸ™ X, inferInstance, Projective.factors f Ï€âŸ©

end Stonean
