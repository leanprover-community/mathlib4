/-
Copyright (c) 2023 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz, Dagur Asgeirsson
-/
import Mathlib.Topology.ExtremallyDisconnected
import Mathlib.Topology.Category.CompHaus.Projective
import Mathlib.Topology.Category.Profinite.Basic
/-!
# Extremally disconnected sets

This file develops some of the basic theory of extremally disconnected compact Hausdorff spaces.

## Overview

This file defines the type `Stonean` of all extremally (note: not "extremely"!)
disconnected compact Hausdorff spaces, gives it the structure of a large category,
and proves some basic observations about this category and various functors from it.

The Lean implementation: a term of type `Stonean` is a pair, considering of
a term of type `CompHaus` (i.e. a compact Hausdorff topological space) plus
a proof that the space is extremally disconnected.
This is equivalent to the assertion that the term is projective in `CompHaus`,
in the sense of category theory (i.e., such that morphisms out of the object
can be lifted along epimorphisms).

## Main definitions

* `Stonean` : the category of extremally disconnected compact Hausdorff spaces.
* `Stonean.toCompHaus` : the forgetful functor `Stonean ‚•§ CompHaus` from Stonean
  spaces to compact Hausdorff spaces
* `Stonean.toProfinite` : the functor from Stonean spaces to profinite spaces.

## Implementation

The category `Stonean` is defined using the structure `CompHausLike`. See the file
`CompHausLike.Basic` for more information.

-/
universe u

open CategoryTheory
open scoped Topology

/-- `Stonean` is the category of extremally disconnected compact Hausdorff spaces. -/
abbrev Stonean := CompHausLike (fun X ‚Ü¶ ExtremallyDisconnected X)

namespace CompHaus

/-- `Projective` implies `ExtremallyDisconnected`. -/
instance (X : CompHaus.{u}) [Projective X] : ExtremallyDisconnected X := by
  apply CompactT2.Projective.extremallyDisconnected
  intro A B _ _ _ _ _ _ f g hf hg hsurj
  let A' : CompHaus := CompHaus.of A
  let B' : CompHaus := CompHaus.of B
  let f' : X ‚ü∂ B' := CompHausLike.ofHom _ ‚ü®f, hf‚ü©
  let g' : A' ‚ü∂ B' := CompHausLike.ofHom _ ‚ü®g,hg‚ü©
  have : Epi g' := by
    rw [CompHaus.epi_iff_surjective]
    assumption
  obtain ‚ü®h, hh‚ü© := Projective.factors f' g'
  refine ‚ü®h, h.hom.2, ?_‚ü©
  ext t
  apply_fun (fun e => e t) at hh
  exact hh

/-- `Projective` implies `Stonean`. -/
@[simps!]
def toStonean (X : CompHaus.{u}) [Projective X] :
    Stonean where
  toTop := X.toTop
  prop := inferInstance

end CompHaus

namespace Stonean

/-- The (forgetful) functor from Stonean spaces to compact Hausdorff spaces. -/
abbrev toCompHaus : Stonean.{u} ‚•§ CompHaus.{u} :=
  compHausLikeToCompHaus _

/-- The forgetful functor `Stonean ‚•§ CompHaus` is fully faithful. -/
abbrev fullyFaithfulToCompHaus : toCompHaus.FullyFaithful  :=
  CompHausLike.fullyFaithfulToCompHausLike _

open CompHausLike

instance (X : Type*) [TopologicalSpace X]
    [ExtremallyDisconnected X] : HasProp (fun Y ‚Ü¶ ExtremallyDisconnected Y) X :=
  ‚ü®(inferInstance : ExtremallyDisconnected X)‚ü©

/-- Construct a term of `Stonean` from a type endowed with the structure of a
compact, Hausdorff and extremally disconnected topological space.
-/
abbrev of (X : Type*) [TopologicalSpace X] [CompactSpace X] [T2Space X]
    [ExtremallyDisconnected X] : Stonean := CompHausLike.of _ X

instance (X : Stonean.{u}) : ExtremallyDisconnected X := X.prop

/-- The functor from Stonean spaces to profinite spaces. -/
abbrev toProfinite : Stonean.{u} ‚•§ Profinite.{u} :=
  CompHausLike.toCompHausLike (fun _ ‚Ü¶ inferInstance)

/--
A finite discrete space as a Stonean space.
-/
def mkFinite (X : Type*) [Finite X] [TopologicalSpace X] [DiscreteTopology X] : Stonean where
  toTop := (CompHaus.of X).toTop
  prop := by
    dsimp
    constructor
    intro U _
    apply isOpen_discrete (closure U)

/--
A morphism in `Stonean` is an epi iff it is surjective.
-/
lemma epi_iff_surjective {X Y : Stonean} (f : X ‚ü∂ Y) :
    Epi f ‚Üî Function.Surjective f := by
  refine ‚ü®?_, fun h => ConcreteCategory.epi_of_surjective f h‚ü©
  dsimp [Function.Surjective]
  intro h y
  by_contra! hy
  let C := Set.range f
  have hC : IsClosed C := (isCompact_range f.hom.continuous).isClosed
  let U := C·∂ú
  have hUy : U ‚àà ùìù y := by
    simp only [U, C, Set.mem_range, hy, exists_false, not_false_eq_true, hC.compl_mem_nhds]
  obtain ‚ü®V, hV, hyV, hVU‚ü© := isTopologicalBasis_isClopen.mem_nhds_iff.mp hUy
  classical
  let g : Y ‚ü∂ mkFinite (ULift (Fin 2)) := TopCat.ofHom
    ‚ü®(LocallyConstant.ofIsClopen hV).map ULift.up, LocallyConstant.continuous _‚ü©
  let h : Y ‚ü∂ mkFinite (ULift (Fin 2)) := TopCat.ofHom ‚ü®fun _ => ‚ü®1‚ü©, continuous_const‚ü©
  have H : h = g := by
    rw [‚Üê cancel_epi f]
    ext x
    apply ULift.ext -- why is `ext` not doing this automatically?
    change 1 = ite _ _ _ -- why is `dsimp` not getting me here?
    rw [if_neg]
    refine mt (hVU ¬∑) ?_ -- what would be an idiomatic tactic for this step?
    simpa only [U, Set.mem_compl_iff, Set.mem_range, not_exists, not_forall, not_not]
      using exists_apply_eq_apply f x
  apply_fun fun e => (e y).down at H
  change 1 = ite _ _ _ at H -- why is `dsimp at H` not getting me here?
  rw [if_pos hyV] at H
  exact one_ne_zero H

/-- Every Stonean space is projective in `CompHaus` -/
instance instProjectiveCompHausCompHaus (X : Stonean) : Projective (toCompHaus.obj X) where
  factors := by
    intro B C œÜ f _
    haveI : ExtremallyDisconnected (toCompHaus.obj X).toTop := X.prop
    have hf : Function.Surjective f := by rwa [‚Üê CompHaus.epi_iff_surjective]
    obtain ‚ü®f', h‚ü© := CompactT2.ExtremallyDisconnected.projective œÜ.hom.continuous f.hom.continuous
      hf
    use ofHom _ ‚ü®f', h.left‚ü©
    ext
    exact congr_fun h.right _

/-- Every Stonean space is projective in `Profinite` -/
instance (X : Stonean) : Projective (toProfinite.obj X) where
  factors := by
    intro B C œÜ f _
    haveI : ExtremallyDisconnected (toProfinite.obj X) := X.prop
    have hf : Function.Surjective f := by rwa [‚Üê Profinite.epi_iff_surjective]
    obtain ‚ü®f', h‚ü© := CompactT2.ExtremallyDisconnected.projective œÜ.hom.continuous f.hom.continuous
      hf
    use ofHom _ ‚ü®f', h.left‚ü©
    ext
    exact congr_fun h.right _

/-- Every Stonean space is projective in `Stonean`. -/
instance (X : Stonean) : Projective X where
  factors := by
    intro B C œÜ f _
    haveI : ExtremallyDisconnected X.toTop := X.prop
    have hf : Function.Surjective f := by rwa [‚Üê Stonean.epi_iff_surjective]
    obtain ‚ü®f', h‚ü© := CompactT2.ExtremallyDisconnected.projective œÜ.hom.continuous f.hom.continuous
      hf
    use ofHom _ ‚ü®f', h.left‚ü©
    ext
    exact congr_fun h.right _

end Stonean

namespace CompHaus

/-- If `X` is compact Hausdorff, `presentation X` is a Stonean space equipped with an epimorphism
  down to `X` (see `CompHaus.presentation.œÄ` and `CompHaus.presentation.epi_œÄ`). It is a
  "constructive" witness to the fact that `CompHaus` has enough projectives. -/
noncomputable
def presentation (X : CompHaus) : Stonean where
  toTop := (projectivePresentation X).p.1
  prop := by
    refine CompactT2.Projective.extremallyDisconnected
      (@fun Y Z _ _ _ _ _ _ f g hfcont hgcont hgsurj => ?_)
    let g‚ÇÅ : (CompHaus.of Y) ‚ü∂ (CompHaus.of Z) := CompHausLike.ofHom _ ‚ü®g, hgcont‚ü©
    let f‚ÇÅ : (projectivePresentation X).p ‚ü∂ (CompHaus.of Z) := CompHausLike.ofHom _ ‚ü®f, hfcont‚ü©
    have hg‚ÇÅ : Epi g‚ÇÅ := (epi_iff_surjective _).2 hgsurj
    refine ‚ü®Projective.factorThru f‚ÇÅ g‚ÇÅ, (Projective.factorThru f‚ÇÅ g‚ÇÅ).hom.2, funext (fun _ => ?_)‚ü©
    change (Projective.factorThru f‚ÇÅ g‚ÇÅ ‚â´ g‚ÇÅ) _ = f _
    rw [Projective.factorThru_comp]
    rfl

/-- The morphism from `presentation X` to `X`. -/
noncomputable
def presentation.œÄ (X : CompHaus) : Stonean.toCompHaus.obj X.presentation ‚ü∂ X :=
  (projectivePresentation X).f

/-- The morphism from `presentation X` to `X` is an epimorphism. -/
noncomputable
instance presentation.epi_œÄ (X : CompHaus) : Epi (œÄ X) :=
  (projectivePresentation X).epi

/-- The underlying `CompHaus` of a `Stonean`. -/
abbrev _root_.Stonean.compHaus (X : Stonean) := Stonean.toCompHaus.obj X

/--
```
               X
               |
              (f)
               |
               \/
  Z ---(e)---> Y
```
If `Z` is a Stonean space, `f : X ‚ü∂ Y` an epi in `CompHaus` and `e : Z ‚ü∂ Y` is arbitrary, then
`lift e f` is a fixed (but arbitrary) lift of `e` to a morphism `Z ‚ü∂ X`. It exists because
`Z` is a projective object in `CompHaus`.
-/
noncomputable
def lift {X Y : CompHaus} {Z : Stonean} (e : Z.compHaus ‚ü∂ Y) (f : X ‚ü∂ Y) [Epi f] :
    Z.compHaus ‚ü∂ X :=
  Projective.factorThru e f

@[simp, reassoc]
lemma lift_lifts {X Y : CompHaus} {Z : Stonean} (e : Z.compHaus ‚ü∂ Y) (f : X ‚ü∂ Y) [Epi f] :
    lift e f ‚â´ f = e := by simp [lift]

lemma Gleason (X : CompHaus.{u}) :
    Projective X ‚Üî ExtremallyDisconnected X := by
  constructor
  ¬∑ intro h
    show ExtremallyDisconnected X.toStonean
    infer_instance
  ¬∑ intro h
    let X' : Stonean := ‚ü®X.toTop, inferInstance‚ü©
    show Projective X'.compHaus
    apply Stonean.instProjectiveCompHausCompHaus

end CompHaus

namespace Profinite

/-- If `X` is profinite, `presentation X` is a Stonean space equipped with an epimorphism down to
    `X` (see `Profinite.presentation.œÄ` and `Profinite.presentation.epi_œÄ`). -/
noncomputable
def presentation (X : Profinite) : Stonean where
  toTop := (profiniteToCompHaus.obj X).projectivePresentation.p.toTop
  prop := (profiniteToCompHaus.obj X).presentation.prop

/-- The morphism from `presentation X` to `X`. -/
noncomputable
def presentation.œÄ (X : Profinite) : Stonean.toProfinite.obj X.presentation ‚ü∂ X :=
  (profiniteToCompHaus.obj X).projectivePresentation.f

/-- The morphism from `presentation X` to `X` is an epimorphism. -/
noncomputable
instance presentation.epi_œÄ (X : Profinite) : Epi (œÄ X) := by
  have := (profiniteToCompHaus.obj X).projectivePresentation.epi
  rw [CompHaus.epi_iff_surjective] at this
  rw [epi_iff_surjective]
  exact this

/--
```
               X
               |
              (f)
               |
               \/
  Z ---(e)---> Y
```
If `Z` is a Stonean space, `f : X ‚ü∂ Y` an epi in `Profinite` and `e : Z ‚ü∂ Y` is arbitrary,
then `lift e f` is a fixed (but arbitrary) lift of `e` to a morphism `Z ‚ü∂ X`. It is
`CompHaus.lift e f` as a morphism in `Profinite`.
-/
noncomputable
def lift {X Y : Profinite} {Z : Stonean} (e : Stonean.toProfinite.obj Z ‚ü∂ Y) (f : X ‚ü∂ Y) [Epi f] :
    Stonean.toProfinite.obj Z ‚ü∂ X := Projective.factorThru e f

@[simp, reassoc]
lemma lift_lifts {X Y : Profinite} {Z : Stonean} (e : Stonean.toProfinite.obj Z ‚ü∂ Y) (f : X ‚ü∂ Y)
    [Epi f] : lift e f ‚â´ f = e := by simp [lift]

lemma projective_of_extrDisc {X : Profinite.{u}} (hX : ExtremallyDisconnected X) :
    Projective X := by
  show Projective (Stonean.toProfinite.obj ‚ü®X.toTop, inferInstance‚ü©)
  exact inferInstance

end Profinite
