/-
Copyright (c) 2017 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Kim Morrison, Mario Carneiro
-/
import Mathlib.CategoryTheory.ConcreteCategory.BundledHom
import Mathlib.Topology.ContinuousMap.Basic

/-!
# Category instance for topological spaces

We introduce the bundled category `TopCat` of topological spaces together with the functors
`TopCat.discrete` and `TopCat.trivial` from the category of types to `TopCat` which equip a type
with the corresponding discrete, resp. trivial, topology. For a proof that these functors are left,
resp. right adjoint to the forgetful functor, see `Mathlib.Topology.Category.TopCat.Adjunctions`.
-/


open CategoryTheory TopologicalSpace Topology

universe u

/-- The category of topological spaces and continuous maps. -/
@[to_additive existing TopCat]
def TopCat : Type (u + 1) :=
  Bundled TopologicalSpace

namespace TopCat

instance bundledHom : BundledHom @ContinuousMap where
  toFun := @ContinuousMap.toFun
  id := @ContinuousMap.id
  comp := @ContinuousMap.comp

deriving instance LargeCategory for TopCat

-- Porting note: currently no derive handler for ConcreteCategory
-- see https://github.com/leanprover-community/mathlib4/issues/5020
instance concreteCategory : ConcreteCategory TopCat :=
  inferInstanceAs <| ConcreteCategory (Bundled TopologicalSpace)

instance : CoeSort TopCat Type* where
  coe X := X.Î±

instance topologicalSpaceUnbundled (X : TopCat) : TopologicalSpace X :=
  X.str

instance instFunLike (X Y : TopCat) : FunLike (X âŸ¶ Y) X Y :=
  inferInstanceAs <| FunLike C(X, Y) X Y

instance instContinuousMapClass (X Y : TopCat) : ContinuousMapClass (X âŸ¶ Y) X Y :=
  inferInstanceAs <| ContinuousMapClass C(X, Y) X Y

@[simp]
theorem id_app (X : TopCat.{u}) (x : â†‘X) : (ðŸ™ X : X âŸ¶ X) x = x := rfl

@[simp]
theorem comp_app {X Y Z : TopCat.{u}} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (x : X) :
    (f â‰« g : X â†’ Z) x = g (f x) := rfl

@[simp] theorem coe_id (X : TopCat.{u}) : (ðŸ™ X : X â†’ X) = id := rfl

@[simp] theorem coe_comp {X Y Z : TopCat.{u}} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    (f â‰« g : X â†’ Z) = g âˆ˜ f := rfl

@[simp]
lemma hom_inv_id_apply {X Y : TopCat} (f : X â‰… Y) (x : X) : f.inv (f.hom x) = x :=
  DFunLike.congr_fun f.hom_inv_id x

@[simp]
lemma inv_hom_id_apply {X Y : TopCat} (f : X â‰… Y) (y : Y) : f.hom (f.inv y) = y :=
  DFunLike.congr_fun f.inv_hom_id y

/-- Construct a bundled `Top` from the underlying type and the typeclass. -/
def of (X : Type u) [TopologicalSpace X] : TopCat :=
  -- Porting note: needed to call inferInstance
  âŸ¨X, inferInstanceâŸ©

instance topologicalSpace_coe (X : TopCat) : TopologicalSpace X :=
  X.str

-- Porting note: cannot see through forget; made reducible to get closer to Lean 3 behavior
@[instance] abbrev topologicalSpace_forget
    (X : TopCat) : TopologicalSpace <| (forget TopCat).obj X :=
  X.str

@[simp]
theorem coe_of (X : Type u) [TopologicalSpace X] : (of X : Type u) = X := rfl

/--
Replace a function coercion for a morphism `TopCat.of X âŸ¶ TopCat.of Y` with the definitionally
equal function coercion for a continuous map `C(X, Y)`.
-/
@[simp] theorem coe_of_of {X Y : Type u} [TopologicalSpace X] [TopologicalSpace Y]
    {f : C(X, Y)} {x} :
    @DFunLike.coe (TopCat.of X âŸ¶ TopCat.of Y) ((CategoryTheory.forget TopCat).obj (TopCat.of X))
      (fun _ â†¦ (CategoryTheory.forget TopCat).obj (TopCat.of Y)) ConcreteCategory.instFunLike
      f x =
    @DFunLike.coe C(X, Y) X
      (fun _ â†¦ Y) _
      f x :=
  rfl

instance inhabited : Inhabited TopCat :=
  âŸ¨TopCat.of EmptyâŸ©

-- Porting note: added to ease the port of `AlgebraicTopology.TopologicalSimplex`
lemma hom_apply {X Y : TopCat} (f : X âŸ¶ Y) (x : X) : f x = ContinuousMap.toFun f x := rfl

/-- The discrete topology on any type. -/
def discrete : Type u â¥¤ TopCat.{u} where
  obj X := âŸ¨X , âŠ¥âŸ©
  map f := @ContinuousMap.mk _ _ âŠ¥ âŠ¥ f continuous_bot

instance {X : Type u} : DiscreteTopology (discrete.obj X) :=
  âŸ¨rflâŸ©

/-- The trivial topology on any type. -/
def trivial : Type u â¥¤ TopCat.{u} where
  obj X := âŸ¨X, âŠ¤âŸ©
  map f := @ContinuousMap.mk _ _ âŠ¤ âŠ¤ f continuous_top

/-- Any homeomorphisms induces an isomorphism in `Top`. -/
@[simps]
def isoOfHomeo {X Y : TopCat.{u}} (f : X â‰ƒâ‚œ Y) : X â‰… Y where
  -- Porting note: previously âŸ¨fâŸ© for hom (inv) and tidy closed proofs
  hom := (f : C(X, Y))
  inv := (f.symm : C(Y, X))
  hom_inv_id := by ext; exact f.symm_apply_apply _
  inv_hom_id := by ext; exact f.apply_symm_apply _

/-- Any isomorphism in `Top` induces a homeomorphism. -/
@[simps]
def homeoOfIso {X Y : TopCat.{u}} (f : X â‰… Y) : X â‰ƒâ‚œ Y where
  toFun := f.hom
  invFun := f.inv
  left_inv x := by simp
  right_inv x := by simp
  continuous_toFun := f.hom.continuous
  continuous_invFun := f.inv.continuous

@[simp]
theorem of_isoOfHomeo {X Y : TopCat.{u}} (f : X â‰ƒâ‚œ Y) : homeoOfIso (isoOfHomeo f) = f := by
  -- Porting note: unfold some defs now
  dsimp [homeoOfIso, isoOfHomeo]
  ext
  rfl

@[simp]
theorem of_homeoOfIso {X Y : TopCat.{u}} (f : X â‰… Y) : isoOfHomeo (homeoOfIso f) = f := by
  -- Porting note: unfold some defs now
  dsimp [homeoOfIso, isoOfHomeo]
  ext
  rfl

lemma isIso_of_bijective_of_isOpenMap {X Y : TopCat.{u}} (f : X âŸ¶ Y)
    (hfbij : Function.Bijective f) (hfcl : IsOpenMap f) : IsIso f :=
  let e : X â‰ƒâ‚œ Y := Homeomorph.homeomorphOfContinuousOpen
    (Equiv.ofBijective f hfbij) f.continuous hfcl
  inferInstanceAs <| IsIso (TopCat.isoOfHomeo e).hom

lemma isIso_of_bijective_of_isClosedMap {X Y : TopCat.{u}} (f : X âŸ¶ Y)
    (hfbij : Function.Bijective f) (hfcl : IsClosedMap f) : IsIso f :=
  let e : X â‰ƒâ‚œ Y := Homeomorph.homeomorphOfContinuousClosed
    (Equiv.ofBijective f hfbij) f.continuous hfcl
  inferInstanceAs <| IsIso (TopCat.isoOfHomeo e).hom

-- Porting note: simpNF requested partially simped version below
theorem isOpenEmbedding_iff_comp_isIso {X Y Z : TopCat} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [IsIso g] :
    IsOpenEmbedding (f â‰« g) â†” IsOpenEmbedding f :=
  (TopCat.homeoOfIso (asIso g)).isOpenEmbedding.of_comp_iff f

@[deprecated (since := "2024-10-18")]
alias openEmbedding_iff_comp_isIso := isOpenEmbedding_iff_comp_isIso

@[simp]
theorem isOpenEmbedding_iff_comp_isIso' {X Y Z : TopCat} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [IsIso g] :
    IsOpenEmbedding ((forget TopCat).map f â‰« (forget TopCat).map g) â†” IsOpenEmbedding f := by
  simp only [â† Functor.map_comp]
  exact isOpenEmbedding_iff_comp_isIso f g

@[deprecated (since := "2024-10-18")]
alias openEmbedding_iff_comp_isIso' := isOpenEmbedding_iff_comp_isIso'

theorem isOpenEmbedding_iff_isIso_comp {X Y Z : TopCat} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [IsIso f] :
    IsOpenEmbedding (f â‰« g) â†” IsOpenEmbedding g := by
  constructor
  Â· intro h
    convert h.comp (TopCat.homeoOfIso (asIso f).symm).isOpenEmbedding
    exact congrArg _ (IsIso.inv_hom_id_assoc f g).symm
  Â· exact fun h â†¦ h.comp (TopCat.homeoOfIso (asIso f)).isOpenEmbedding

@[deprecated (since := "2024-10-18")]
alias openEmbedding_iff_isIso_comp := isOpenEmbedding_iff_isIso_comp

@[simp]
theorem isOpenEmbedding_iff_isIso_comp' {X Y Z : TopCat} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [IsIso f] :
    IsOpenEmbedding ((forget TopCat).map f â‰« (forget TopCat).map g) â†” IsOpenEmbedding g := by
  simp only [â† Functor.map_comp]
  exact isOpenEmbedding_iff_isIso_comp f g

@[deprecated (since := "2024-10-18")]
alias openEmbedding_iff_isIso_comp' := isOpenEmbedding_iff_isIso_comp'

end TopCat
