/-
Copyright (c) 2017 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Scott Morrison, Mario Carneiro
-/
import Mathlib.CategoryTheory.ConcreteCategory.BundledHom
import Mathlib.CategoryTheory.Elementwise
import Mathlib.Topology.ContinuousFunction.Basic

#align_import topology.category.Top.basic from "leanprover-community/mathlib"@"bcfa726826abd57587355b4b5b7e78ad6527b7e4"

/-!
# Category instance for topological spaces

We introduce the bundled category `TopCat` of topological spaces together with the functors
`discrete` and `trivial` from the category of types to `TopCat` which equip a type with the
corresponding discrete, resp. trivial, topology. For a proof that these functors are left,
resp. right adjoint to the forgetful functor, see `topology.category.TopCat.adjunctions`.
-/


open CategoryTheory

open TopologicalSpace

universe u

/-- The category of topological spaces and continuous maps. -/
@[to_additive existing TopCat]
def TopCat : Type (u + 1) :=
  Bundled TopologicalSpace
set_option linter.uppercaseLean3 false in
#align Top TopCat

namespace TopCat

-- porting note: had to add in the last two proofs
instance bundledHom : BundledHom @ContinuousMap :=
  ‚ü®@ContinuousMap.toFun, @ContinuousMap.id, @ContinuousMap.comp, @ContinuousMap.coe_injective,
    fun _ => rfl, fun _ _ _ _ _ => rfl‚ü©
set_option linter.uppercaseLean3 false in
#align Top.bundled_hom TopCat.bundledHom

deriving instance LargeCategory for TopCat

-- Porting note: currently no derive handler for ConcreteCategory
-- see https://github.com/leanprover-community/mathlib4/issues/5020
instance concreteCategory : ConcreteCategory TopCat := by
  dsimp [TopCat]
  infer_instance

@[to_additive existing TopCat.instCoeSortTopCatType]
instance instCoeSortTopCatType : CoeSort TopCat (Type*) :=
  Bundled.coeSort

instance topologicalSpaceUnbundled (x : TopCat) : TopologicalSpace x :=
  x.str
set_option linter.uppercaseLean3 false in
#align Top.topological_space_unbundled TopCat.topologicalSpaceUnbundled

-- Porting note: cannot find a coercion to function otherwise
attribute [instance] ConcreteCategory.funLike in
instance (X Y : TopCat.{u}) : CoeFun (X ‚ü∂ Y) fun _ => X ‚Üí Y where
  coe f := f

-- Porting note: simp can prove this; removed simp
theorem id_app (X : TopCat.{u}) (x : ‚ÜëX) : (ùüô X : X ‚ü∂ X) x = x := rfl
set_option linter.uppercaseLean3 false in
#align Top.id_app TopCat.id_app

-- Porting note: simp can prove this; removed simp
theorem comp_app {X Y Z : TopCat.{u}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (x : X) :
    (f ‚â´ g : X ‚Üí Z) x = g (f x) := rfl
set_option linter.uppercaseLean3 false in
#align Top.comp_app TopCat.comp_app

/-- Construct a bundled `Top` from the underlying type and the typeclass. -/
def of (X : Type u) [TopologicalSpace X] : TopCat :=
  -- Porting note: needed to call inferInstance
  ‚ü®X, inferInstance‚ü©
set_option linter.uppercaseLean3 false in
#align Top.of TopCat.of

instance topologicalSpace_coe (X : TopCat) : TopologicalSpace X :=
  X.str

-- Porting note: cannot see through forget; made reducible to get closer to Lean 3 behavior
@[reducible]
instance topologicalSpace_forget (X : TopCat) : TopologicalSpace <| (forget TopCat).obj X :=
  X.str

@[simp]
theorem coe_of (X : Type u) [TopologicalSpace X] : (of X : Type u) = X := rfl
set_option linter.uppercaseLean3 false in
#align Top.coe_of TopCat.coe_of

instance inhabited : Inhabited TopCat :=
  ‚ü®TopCat.of Empty‚ü©

-- porting note: added to ease the port of `AlgebraicTopology.TopologicalSimplex`
lemma hom_apply {X Y : TopCat} (f : X ‚ü∂ Y) (x : X) : f x = ContinuousMap.toFun f x := rfl

/-- The discrete topology on any type. -/
def discrete : Type u ‚•§ TopCat.{u} where
  obj X := ‚ü®X , ‚ä•‚ü©
  map f := @ContinuousMap.mk _ _ ‚ä• ‚ä• f continuous_bot
set_option linter.uppercaseLean3 false in
#align Top.discrete TopCat.discrete

instance {X : Type u} : DiscreteTopology (discrete.obj X) :=
  ‚ü®rfl‚ü©

/-- The trivial topology on any type. -/
def trivial : Type u ‚•§ TopCat.{u} where
  obj X := ‚ü®X, ‚ä§‚ü©
  map f := @ContinuousMap.mk _ _ ‚ä§ ‚ä§ f continuous_top
set_option linter.uppercaseLean3 false in
#align Top.trivial TopCat.trivial

/-- Any homeomorphisms induces an isomorphism in `Top`. -/
@[simps]
def isoOfHomeo {X Y : TopCat.{u}} (f : X ‚âÉ‚Çú Y) : X ‚âÖ Y where
  -- Porting note: previously ‚ü®f‚ü© for hom (inv) and tidy closed proofs
  hom := f.toContinuousMap
  inv := f.symm.toContinuousMap
  hom_inv_id := by ext; exact f.symm_apply_apply _
  inv_hom_id := by ext; exact f.apply_symm_apply _
set_option linter.uppercaseLean3 false in
#align Top.iso_of_homeo TopCat.isoOfHomeo

/-- Any isomorphism in `Top` induces a homeomorphism. -/
@[simps]
def homeoOfIso {X Y : TopCat.{u}} (f : X ‚âÖ Y) : X ‚âÉ‚Çú Y where
  toFun := f.hom
  invFun := f.inv
  left_inv x := by simp
  right_inv x := by simp
  continuous_toFun := f.hom.continuous
  continuous_invFun := f.inv.continuous
set_option linter.uppercaseLean3 false in
#align Top.homeo_of_iso TopCat.homeoOfIso

@[simp]
theorem of_isoOfHomeo {X Y : TopCat.{u}} (f : X ‚âÉ‚Çú Y) : homeoOfIso (isoOfHomeo f) = f := by
  -- Porting note: unfold some defs now
  dsimp [homeoOfIso, isoOfHomeo]
  ext
  rfl
set_option linter.uppercaseLean3 false in
#align Top.of_iso_of_homeo TopCat.of_isoOfHomeo

@[simp]
theorem of_homeoOfIso {X Y : TopCat.{u}} (f : X ‚âÖ Y) : isoOfHomeo (homeoOfIso f) = f := by
  -- Porting note: unfold some defs now
  dsimp [homeoOfIso, isoOfHomeo]
  ext
  rfl
set_option linter.uppercaseLean3 false in
#align Top.of_homeo_of_iso TopCat.of_homeoOfIso

-- Porting note: simpNF requested partially simped version below
theorem openEmbedding_iff_comp_isIso {X Y Z : TopCat} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [IsIso g] :
    OpenEmbedding (f ‚â´ g) ‚Üî OpenEmbedding f :=
  (TopCat.homeoOfIso (asIso g)).openEmbedding.of_comp_iff f
set_option linter.uppercaseLean3 false in
#align Top.open_embedding_iff_comp_is_iso TopCat.openEmbedding_iff_comp_isIso

@[simp]
theorem openEmbedding_iff_comp_isIso' {X Y Z : TopCat} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [IsIso g] :
    OpenEmbedding ((forget TopCat).map f ‚â´ (forget TopCat).map g) ‚Üî OpenEmbedding f := by
  simp only [‚ÜêFunctor.map_comp]
  exact openEmbedding_iff_comp_isIso f g

-- Porting note: simpNF requested partially simped version below
theorem openEmbedding_iff_isIso_comp {X Y Z : TopCat} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [IsIso f] :
    OpenEmbedding (f ‚â´ g) ‚Üî OpenEmbedding g := by
  constructor
  ¬∑ intro h
    convert h.comp (TopCat.homeoOfIso (asIso f).symm).openEmbedding
    exact congrArg _ (IsIso.inv_hom_id_assoc f g).symm
  ¬∑ exact fun h => h.comp (TopCat.homeoOfIso (asIso f)).openEmbedding
set_option linter.uppercaseLean3 false in
#align Top.open_embedding_iff_is_iso_comp TopCat.openEmbedding_iff_isIso_comp

@[simp]
theorem openEmbedding_iff_isIso_comp' {X Y Z : TopCat} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [IsIso f] :
    OpenEmbedding ((forget TopCat).map f ‚â´ (forget TopCat).map g) ‚Üî OpenEmbedding g := by
  simp only [‚ÜêFunctor.map_comp]
  exact openEmbedding_iff_isIso_comp f g

end TopCat
