/-
Copyright (c) 2017 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Kim Morrison, Mario Carneiro, Andrew Yang
-/
import Mathlib.Topology.Category.TopCat.Limits.Products

/-!
# Pullbacks and pushouts in the category of topological spaces
-/

open TopologicalSpace Topology

open CategoryTheory

open CategoryTheory.Limits

universe v u w

noncomputable section

namespace TopCat

variable {J : Type v} [Category.{w} J]

section Pullback

variable {X Y Z : TopCat.{u}}

/-- The first projection from the pullback. -/
abbrev pullbackFst (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) : TopCat.of { p : X √ó Y // f p.1 = g p.2 } ‚ü∂ X :=
  ofHom ‚ü®Prod.fst ‚àò Subtype.val, by fun_prop‚ü©

lemma pullbackFst_apply (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) (x) : pullbackFst f g x = x.1.1 := rfl

/-- The second projection from the pullback. -/
abbrev pullbackSnd (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) : TopCat.of { p : X √ó Y // f p.1 = g p.2 } ‚ü∂ Y :=
  ofHom ‚ü®Prod.snd ‚àò Subtype.val, by fun_prop‚ü©

lemma pullbackSnd_apply (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) (x) : pullbackSnd f g x = x.1.2 := rfl

/-- The explicit pullback cone of `X, Y` given by `{ p : X √ó Y // f p.1 = g p.2 }`. -/
def pullbackCone (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) : PullbackCone f g :=
  PullbackCone.mk (pullbackFst f g) (pullbackSnd f g)
    (by
      dsimp [pullbackFst, pullbackSnd, Function.comp_def]
      ext ‚ü®x, h‚ü©
      simpa)

/-- The constructed cone is a limit. -/
def pullbackConeIsLimit (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) : IsLimit (pullbackCone f g) :=
  PullbackCone.isLimitAux' _
    (by
      intro S
      constructor; swap
      ¬∑ exact ofHom
          { toFun := fun x =>
              ‚ü®‚ü®S.fst x, S.snd x‚ü©, by simpa using ConcreteCategory.congr_hom S.condition x‚ü©
            continuous_toFun := by fun_prop }
      refine ‚ü®?_, ?_, ?_‚ü©
      ¬∑ delta pullbackCone
        ext a
        dsimp
      ¬∑ delta pullbackCone
        ext a
        dsimp
      ¬∑ intro m h‚ÇÅ h‚ÇÇ
        ext x
        -- Porting note (https://github.com/leanprover-community/mathlib4/issues/11041): used to be `ext x`.
        apply Subtype.ext
        apply Prod.ext
        ¬∑ simpa using ConcreteCategory.congr_hom h‚ÇÅ x
        ¬∑ simpa using ConcreteCategory.congr_hom h‚ÇÇ x)

/-- The pullback of two maps can be identified as a subspace of `X √ó Y`. -/
def pullbackIsoProdSubtype (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :
    pullback f g ‚âÖ TopCat.of { p : X √ó Y // f p.1 = g p.2 } :=
  (limit.isLimit _).conePointUniqueUpToIso (pullbackConeIsLimit f g)

@[reassoc (attr := simp)]
theorem pullbackIsoProdSubtype_inv_fst (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :
    (pullbackIsoProdSubtype f g).inv ‚â´ pullback.fst _ _ = pullbackFst f g := by
  simp [pullbackCone, pullbackIsoProdSubtype]

theorem pullbackIsoProdSubtype_inv_fst_apply (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z)
    (x : { p : X √ó Y // f p.1 = g p.2 }) :
    pullback.fst f g ((pullbackIsoProdSubtype f g).inv x) = (x : X √ó Y).fst :=
  ConcreteCategory.congr_hom (pullbackIsoProdSubtype_inv_fst f g) x

@[reassoc (attr := simp)]
theorem pullbackIsoProdSubtype_inv_snd (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :
    (pullbackIsoProdSubtype f g).inv ‚â´ pullback.snd _ _ = pullbackSnd f g := by
  simp [pullbackCone, pullbackIsoProdSubtype]

theorem pullbackIsoProdSubtype_inv_snd_apply (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z)
    (x : { p : X √ó Y // f p.1 = g p.2 }) :
    pullback.snd f g ((pullbackIsoProdSubtype f g).inv x) = (x : X √ó Y).snd :=
  ConcreteCategory.congr_hom (pullbackIsoProdSubtype_inv_snd f g) x

theorem pullbackIsoProdSubtype_hom_fst (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :
    (pullbackIsoProdSubtype f g).hom ‚â´ pullbackFst f g = pullback.fst _ _ := by
  rw [‚Üê Iso.eq_inv_comp, pullbackIsoProdSubtype_inv_fst]

theorem pullbackIsoProdSubtype_hom_snd (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :
    (pullbackIsoProdSubtype f g).hom ‚â´ pullbackSnd f g = pullback.snd _ _ := by
  rw [‚Üê Iso.eq_inv_comp, pullbackIsoProdSubtype_inv_snd]

theorem pullbackIsoProdSubtype_hom_apply {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z}
    (x : ‚Üë(pullback f g)) :
    (pullbackIsoProdSubtype f g).hom x =
      ‚ü®‚ü®pullback.fst f g x, pullback.snd f g x‚ü©, by
        simpa using CategoryTheory.congr_fun pullback.condition x‚ü© := rfl

theorem pullback_topology {X Y Z : TopCat.{u}} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :
    (pullback f g).str =
      induced (pullback.fst f g) X.str ‚äì
        induced (pullback.snd f g) Y.str := by
  let homeo := homeoOfIso (pullbackIsoProdSubtype f g)
  refine homeo.isInducing.eq_induced.trans ?_
  change induced homeo (induced _ ( (induced Prod.fst X.str) ‚äì (induced Prod.snd Y.str))) = _
  simp only [induced_compose, induced_inf]
  rfl

theorem range_pullback_to_prod {X Y Z : TopCat} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :
    Set.range (prod.lift (pullback.fst f g) (pullback.snd f g)) =
      { x | (Limits.prod.fst ‚â´ f) x = (Limits.prod.snd ‚â´ g) x } := by
  ext x
  constructor
  ¬∑ rintro ‚ü®y, rfl‚ü©
    simp only [‚Üê ConcreteCategory.comp_apply, Set.mem_setOf_eq]
    simp [pullback.condition]
  ¬∑ rintro (h : f (_, _).1 = g (_, _).2)
    use (pullbackIsoProdSubtype f g).inv ‚ü®‚ü®_, _‚ü©, h‚ü©
    apply Concrete.limit_ext
    rintro ‚ü®‚ü®‚ü©‚ü© <;>
      rw [‚Üê ConcreteCategory.comp_apply, ‚Üê ConcreteCategory.comp_apply, limit.lift_œÄ] <;>
      -- This used to be `simp` before https://github.com/leanprover/lean4/pull/2644
      cat_disch

/-- The pullback along an embedding is (isomorphic to) the preimage. -/
noncomputable
def pullbackHomeoPreimage
    {X Y Z : Type*} [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]
    (f : X ‚Üí Z) (hf : Continuous f) (g : Y ‚Üí Z) (hg : IsEmbedding g) :
    { p : X √ó Y // f p.1 = g p.2 } ‚âÉ‚Çú f ‚Åª¬π' Set.range g where
  toFun := fun x ‚Ü¶ ‚ü®x.1.1, _, x.2.symm‚ü©
  invFun := fun x ‚Ü¶ ‚ü®‚ü®x.1, Exists.choose x.2‚ü©, (Exists.choose_spec x.2).symm‚ü©
  left_inv := by
    intro x
    ext <;> dsimp
    apply hg.injective
    convert x.prop
    exact Exists.choose_spec (p := fun y ‚Ü¶ g y = f (‚Üëx : X √ó Y).1) _
  continuous_toFun := by fun_prop
  continuous_invFun := by
    apply Continuous.subtype_mk
    refine continuous_subtype_val.prodMk <| hg.isInducing.continuous_iff.mpr ?_
    convert hf.comp continuous_subtype_val
    ext x
    exact Exists.choose_spec x.2

theorem isInducing_pullback_to_prod {X Y Z : TopCat.{u}} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :
    IsInducing <| ‚áë(prod.lift (pullback.fst f g) (pullback.snd f g)) :=
  ‚ü®by simp [prod_topology, pullback_topology, induced_compose, ‚Üê coe_comp]‚ü©

theorem isEmbedding_pullback_to_prod {X Y Z : TopCat.{u}} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :
    IsEmbedding <| ‚áë(prod.lift (pullback.fst f g) (pullback.snd f g)) :=
  ‚ü®isInducing_pullback_to_prod f g, (TopCat.mono_iff_injective _).mp inferInstance‚ü©

/-- If the map `S ‚ü∂ T` is mono, then there is a description of the image of `W √ó‚Çõ X ‚ü∂ Y √ó‚Çú Z`. -/
theorem range_pullback_map {W X Y Z S T : TopCat} (f‚ÇÅ : W ‚ü∂ S) (f‚ÇÇ : X ‚ü∂ S) (g‚ÇÅ : Y ‚ü∂ T)
    (g‚ÇÇ : Z ‚ü∂ T) (i‚ÇÅ : W ‚ü∂ Y) (i‚ÇÇ : X ‚ü∂ Z) (i‚ÇÉ : S ‚ü∂ T) [H‚ÇÉ : Mono i‚ÇÉ] (eq‚ÇÅ : f‚ÇÅ ‚â´ i‚ÇÉ = i‚ÇÅ ‚â´ g‚ÇÅ)
    (eq‚ÇÇ : f‚ÇÇ ‚â´ i‚ÇÉ = i‚ÇÇ ‚â´ g‚ÇÇ) :
    Set.range (pullback.map f‚ÇÅ f‚ÇÇ g‚ÇÅ g‚ÇÇ i‚ÇÅ i‚ÇÇ i‚ÇÉ eq‚ÇÅ eq‚ÇÇ) =
      (pullback.fst g‚ÇÅ g‚ÇÇ) ‚Åª¬π' Set.range i‚ÇÅ ‚à© (pullback.snd g‚ÇÅ g‚ÇÇ) ‚Åª¬π' Set.range i‚ÇÇ := by
  ext
  constructor
  ¬∑ rintro ‚ü®y, rfl‚ü©
    simp only [Set.mem_inter_iff, Set.mem_preimage, Set.mem_range]
    rw [‚Üê ConcreteCategory.comp_apply, ‚Üê ConcreteCategory.comp_apply]
    simp only [limit.lift_œÄ, PullbackCone.mk_pt, PullbackCone.mk_œÄ_app]
    exact ‚ü®exists_apply_eq_apply _ _, exists_apply_eq_apply _ _‚ü©
  rintro ‚ü®‚ü®x‚ÇÅ, hx‚ÇÅ‚ü©, ‚ü®x‚ÇÇ, hx‚ÇÇ‚ü©‚ü©
  have : f‚ÇÅ x‚ÇÅ = f‚ÇÇ x‚ÇÇ := by
    apply (TopCat.mono_iff_injective _).mp H‚ÇÉ
    rw [‚Üê ConcreteCategory.comp_apply, eq‚ÇÅ, ‚Üê ConcreteCategory.comp_apply, eq‚ÇÇ,
      ConcreteCategory.comp_apply, ConcreteCategory.comp_apply, hx‚ÇÅ, hx‚ÇÇ,
      ‚Üê ConcreteCategory.comp_apply, pullback.condition, ConcreteCategory.comp_apply]
  use (pullbackIsoProdSubtype f‚ÇÅ f‚ÇÇ).inv ‚ü®‚ü®x‚ÇÅ, x‚ÇÇ‚ü©, this‚ü©
  apply Concrete.limit_ext
  rintro (_ | _ | _) <;>
  rw [‚Üê ConcreteCategory.comp_apply, ‚Üê ConcreteCategory.comp_apply]
  ¬∑ simp [hx‚ÇÅ, ‚Üê limit.w _ WalkingCospan.Hom.inl]
  ¬∑ simp [hx‚ÇÅ]
  ¬∑ simp [hx‚ÇÇ]

theorem pullback_fst_range {X Y S : TopCat} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) :
    Set.range (pullback.fst f g) = { x : X | ‚àÉ y : Y, f x = g y } := by
  ext x
  constructor
  ¬∑ rintro ‚ü®y, rfl‚ü©
    use pullback.snd f g y
    exact CategoryTheory.congr_fun pullback.condition y
  ¬∑ rintro ‚ü®y, eq‚ü©
    use (TopCat.pullbackIsoProdSubtype f g).inv ‚ü®‚ü®x, y‚ü©, eq‚ü©
    rw [pullbackIsoProdSubtype_inv_fst_apply]

theorem pullback_snd_range {X Y S : TopCat} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) :
    Set.range (pullback.snd f g) = { y : Y | ‚àÉ x : X, f x = g y } := by
  ext y
  constructor
  ¬∑ rintro ‚ü®x, rfl‚ü©
    use pullback.fst f g x
    exact CategoryTheory.congr_fun pullback.condition x
  ¬∑ rintro ‚ü®x, eq‚ü©
    use (TopCat.pullbackIsoProdSubtype f g).inv ‚ü®‚ü®x, y‚ü©, eq‚ü©
    rw [pullbackIsoProdSubtype_inv_snd_apply]

/-- If there is a diagram where the morphisms `W ‚ü∂ Y` and `X ‚ü∂ Z` are embeddings,
then the induced morphism `W √ó‚Çõ X ‚ü∂ Y √ó‚Çú Z` is also an embedding.

```
W ‚ü∂ Y
 ‚Üò   ‚Üò
  S ‚ü∂ T
 ‚Üó   ‚Üó
X ‚ü∂ Z
```
-/
theorem pullback_map_isEmbedding {W X Y Z S T : TopCat.{u}} (f‚ÇÅ : W ‚ü∂ S) (f‚ÇÇ : X ‚ü∂ S)
    (g‚ÇÅ : Y ‚ü∂ T) (g‚ÇÇ : Z ‚ü∂ T) {i‚ÇÅ : W ‚ü∂ Y} {i‚ÇÇ : X ‚ü∂ Z} (H‚ÇÅ : IsEmbedding i‚ÇÅ)
    (H‚ÇÇ : IsEmbedding i‚ÇÇ) (i‚ÇÉ : S ‚ü∂ T) (eq‚ÇÅ : f‚ÇÅ ‚â´ i‚ÇÉ = i‚ÇÅ ‚â´ g‚ÇÅ) (eq‚ÇÇ : f‚ÇÇ ‚â´ i‚ÇÉ = i‚ÇÇ ‚â´ g‚ÇÇ) :
    IsEmbedding (pullback.map f‚ÇÅ f‚ÇÇ g‚ÇÅ g‚ÇÇ i‚ÇÅ i‚ÇÇ i‚ÇÉ eq‚ÇÅ eq‚ÇÇ) := by
  refine .of_comp (ContinuousMap.continuous_toFun _)
    (show Continuous (prod.lift (pullback.fst g‚ÇÅ g‚ÇÇ) (pullback.snd g‚ÇÅ g‚ÇÇ)) from
        ContinuousMap.continuous_toFun _)
      ?_
  suffices
    IsEmbedding (prod.lift (pullback.fst f‚ÇÅ f‚ÇÇ) (pullback.snd f‚ÇÅ f‚ÇÇ) ‚â´ Limits.prod.map i‚ÇÅ i‚ÇÇ) by
    simpa [‚Üê coe_comp] using this
  rw [coe_comp]
  exact (isEmbedding_prodMap H‚ÇÅ H‚ÇÇ).comp (isEmbedding_pullback_to_prod _ _)

/-- If there is a diagram where the morphisms `W ‚ü∂ Y` and `X ‚ü∂ Z` are open embeddings, and `S ‚ü∂ T`
is mono, then the induced morphism `W √ó‚Çõ X ‚ü∂ Y √ó‚Çú Z` is also an open embedding.

```
W ‚ü∂ Y
 ‚Üò   ‚Üò
  S ‚ü∂ T
 ‚Üó   ‚Üó
X ‚ü∂ Z
```
-/
theorem pullback_map_isOpenEmbedding {W X Y Z S T : TopCat.{u}} (f‚ÇÅ : W ‚ü∂ S)
    (f‚ÇÇ : X ‚ü∂ S) (g‚ÇÅ : Y ‚ü∂ T) (g‚ÇÇ : Z ‚ü∂ T) {i‚ÇÅ : W ‚ü∂ Y} {i‚ÇÇ : X ‚ü∂ Z} (H‚ÇÅ : IsOpenEmbedding i‚ÇÅ)
    (H‚ÇÇ : IsOpenEmbedding i‚ÇÇ) (i‚ÇÉ : S ‚ü∂ T) [H‚ÇÉ : Mono i‚ÇÉ] (eq‚ÇÅ : f‚ÇÅ ‚â´ i‚ÇÉ = i‚ÇÅ ‚â´ g‚ÇÅ)
    (eq‚ÇÇ : f‚ÇÇ ‚â´ i‚ÇÉ = i‚ÇÇ ‚â´ g‚ÇÇ) : IsOpenEmbedding (pullback.map f‚ÇÅ f‚ÇÇ g‚ÇÅ g‚ÇÇ i‚ÇÅ i‚ÇÇ i‚ÇÉ eq‚ÇÅ eq‚ÇÇ) := by
  constructor
  ¬∑ apply
      pullback_map_isEmbedding f‚ÇÅ f‚ÇÇ g‚ÇÅ g‚ÇÇ H‚ÇÅ.isEmbedding H‚ÇÇ.isEmbedding i‚ÇÉ eq‚ÇÅ eq‚ÇÇ
  ¬∑ rw [range_pullback_map]
    apply IsOpen.inter <;> apply Continuous.isOpen_preimage
    ¬∑ apply ContinuousMap.continuous_toFun
    ¬∑ exact H‚ÇÅ.isOpen_range
    ¬∑ apply ContinuousMap.continuous_toFun
    ¬∑ exact H‚ÇÇ.isOpen_range


lemma snd_isEmbedding_of_left {X Y S : TopCat} {f : X ‚ü∂ S} (H : IsEmbedding f) (g : Y ‚ü∂ S) :
    IsEmbedding <| ‚áë(pullback.snd f g) := by
  convert (homeoOfIso (asIso (pullback.snd (ùüô S) g))).isEmbedding.comp
      (pullback_map_isEmbedding (i‚ÇÇ := ùüô Y)
        f g (ùüô S) g H (homeoOfIso (Iso.refl _)).isEmbedding (ùüô _) rfl (by simp))
  simp [homeoOfIso, ‚Üê coe_comp]

theorem fst_isEmbedding_of_right {X Y S : TopCat} (f : X ‚ü∂ S) {g : Y ‚ü∂ S}
    (H : IsEmbedding g) : IsEmbedding <| ‚áë(pullback.fst f g) := by
  convert (homeoOfIso (asIso (pullback.fst f (ùüô S)))).isEmbedding.comp
      (pullback_map_isEmbedding (i‚ÇÅ := ùüô X)
        f g f (ùüô _) (homeoOfIso (Iso.refl _)).isEmbedding H (ùüô _) rfl (by simp))
  simp [homeoOfIso, ‚Üê coe_comp]

theorem isEmbedding_of_pullback {X Y S : TopCat} {f : X ‚ü∂ S} {g : Y ‚ü∂ S} (H‚ÇÅ : IsEmbedding f)
    (H‚ÇÇ : IsEmbedding g) : IsEmbedding (limit.œÄ (cospan f g) WalkingCospan.one) := by
  convert H‚ÇÇ.comp (snd_isEmbedding_of_left H‚ÇÅ g)
  rw [‚Üê coe_comp, ‚Üê limit.w _ WalkingCospan.Hom.inr]
  rfl

theorem snd_isOpenEmbedding_of_left {X Y S : TopCat} {f : X ‚ü∂ S} (H : IsOpenEmbedding f)
    (g : Y ‚ü∂ S) : IsOpenEmbedding <| ‚áë(pullback.snd f g) := by
  convert (homeoOfIso (asIso (pullback.snd (ùüô S) g))).isOpenEmbedding.comp
      (pullback_map_isOpenEmbedding (i‚ÇÇ := ùüô Y) f g (ùüô _) g H
        (homeoOfIso (Iso.refl _)).isOpenEmbedding (ùüô _) rfl (by simp))
  simp [homeoOfIso, ‚Üê coe_comp]

theorem fst_isOpenEmbedding_of_right {X Y S : TopCat} (f : X ‚ü∂ S) {g : Y ‚ü∂ S}
    (H : IsOpenEmbedding g) : IsOpenEmbedding <| ‚áë(pullback.fst f g) := by
  convert (homeoOfIso (asIso (pullback.fst f (ùüô S)))).isOpenEmbedding.comp
      (pullback_map_isOpenEmbedding (i‚ÇÅ := ùüô X) f g f (ùüô _)
        (homeoOfIso (Iso.refl _)).isOpenEmbedding H (ùüô _) rfl (by simp))
  simp [homeoOfIso, ‚Üê coe_comp]

/-- If `X ‚ü∂ S`, `Y ‚ü∂ S` are open embeddings, then so is `X √ó‚Çõ Y ‚ü∂ S`. -/
theorem isOpenEmbedding_of_pullback {X Y S : TopCat} {f : X ‚ü∂ S} {g : Y ‚ü∂ S}
    (H‚ÇÅ : IsOpenEmbedding f) (H‚ÇÇ : IsOpenEmbedding g) :
    IsOpenEmbedding (limit.œÄ (cospan f g) WalkingCospan.one) := by
  convert H‚ÇÇ.comp (snd_isOpenEmbedding_of_left H‚ÇÅ g)
  rw [‚Üê coe_comp, ‚Üê limit.w _ WalkingCospan.Hom.inr]
  rfl

theorem fst_iso_of_right_embedding_range_subset {X Y S : TopCat} (f : X ‚ü∂ S) {g : Y ‚ü∂ S}
    (hg : IsEmbedding g) (H : Set.range f ‚äÜ Set.range g) :
    IsIso (pullback.fst f g) := by
  let esto : (pullback f g : TopCat) ‚âÉ‚Çú X :=
    (fst_isEmbedding_of_right f hg).toHomeomorph.trans
      { toFun := Subtype.val
        invFun := fun x =>
          ‚ü®x, by
            rw [pullback_fst_range]
            exact ‚ü®_, (H (Set.mem_range_self x)).choose_spec.symm‚ü©‚ü© }
  convert (isoOfHomeo esto).isIso_hom

theorem snd_iso_of_left_embedding_range_subset {X Y S : TopCat} {f : X ‚ü∂ S} (hf : IsEmbedding f)
    (g : Y ‚ü∂ S) (H : Set.range g ‚äÜ Set.range f) : IsIso (pullback.snd f g) := by
  let esto : (pullback f g : TopCat) ‚âÉ‚Çú Y :=
    (snd_isEmbedding_of_left hf g).toHomeomorph.trans
      { toFun := Subtype.val
        invFun := fun x =>
          ‚ü®x, by
            rw [pullback_snd_range]
            exact ‚ü®_, (H (Set.mem_range_self x)).choose_spec‚ü©‚ü© }
  convert (isoOfHomeo esto).isIso_hom

theorem pullback_snd_image_fst_preimage (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) (U : Set X) :
    (pullback.snd f g) '' ((pullback.fst f g) ‚Åª¬π' U) =
      g ‚Åª¬π' (f '' U) := by
  ext x
  constructor
  ¬∑ rintro ‚ü®y, hy, rfl‚ü©
    exact
      ‚ü®(pullback.fst f g) y, hy, CategoryTheory.congr_fun pullback.condition y‚ü©
  ¬∑ rintro ‚ü®y, hy, eq‚ü©
  -- next 5 lines were
  -- `exact ‚ü®(TopCat.pullbackIsoProdSubtype f g).inv ‚ü®‚ü®_, _‚ü©, eq‚ü©, by simpa, by simp‚ü©` before https://github.com/leanprover-community/mathlib4/pull/13170
    refine ‚ü®(TopCat.pullbackIsoProdSubtype f g).inv ‚ü®‚ü®_, _‚ü©, eq‚ü©, ?_, ?_‚ü©
    ¬∑ simp only [coe_of, Set.mem_preimage]
      convert hy
      rw [pullbackIsoProdSubtype_inv_fst_apply]
    ¬∑ rw [pullbackIsoProdSubtype_inv_snd_apply]

theorem pullback_fst_image_snd_preimage (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) (U : Set Y) :
    (pullback.fst f g) '' ((pullback.snd f g) ‚Åª¬π' U) =
      f ‚Åª¬π' (g '' U) := by
  ext x
  constructor
  ¬∑ rintro ‚ü®y, hy, rfl‚ü©
    exact
      ‚ü®(pullback.snd f g) y, hy,
        (CategoryTheory.congr_fun pullback.condition y).symm‚ü©
  ¬∑ rintro ‚ü®y, hy, eq‚ü©
    -- next 5 lines were
    -- `exact ‚ü®(TopCat.pullbackIsoProdSubtype f g).inv ‚ü®‚ü®_, _‚ü©, eq.symm‚ü©, by simpa, by simp‚ü©`
    -- before https://github.com/leanprover-community/mathlib4/pull/13170
    refine ‚ü®(TopCat.pullbackIsoProdSubtype f g).inv ‚ü®‚ü®_, _‚ü©, eq.symm‚ü©, ?_, ?_‚ü©
    ¬∑ simp only [coe_of, Set.mem_preimage]
      convert hy
      rw [pullbackIsoProdSubtype_inv_snd_apply]
    ¬∑ rw [pullbackIsoProdSubtype_inv_fst_apply]

end Pullback

section

variable {X Y : TopCat.{u}} {f g : X ‚ü∂ Y}

lemma isOpen_iff_of_isColimit_cofork (c : Cofork f g) (hc : IsColimit c) (U : Set c.pt) :
    IsOpen U ‚Üî IsOpen (c.œÄ ‚Åª¬π' U) := by
  rw [isOpen_iff_of_isColimit _ hc]
  constructor
  ¬∑ intro h
    exact h .one
  ¬∑ rintro h (_ | _)
    ¬∑ rw [‚Üê c.w .left]
      exact Continuous.isOpen_preimage f.hom.continuous (c.œÄ ‚Åª¬π' U) h
    ¬∑ exact h

lemma isQuotientMap_of_isColimit_cofork (c : Cofork f g) (hc : IsColimit c) :
    IsQuotientMap c.œÄ := by
  rw [isQuotientMap_iff]
  constructor
  ¬∑ simpa only [‚Üê epi_iff_surjective] using epi_of_isColimit_cofork hc
  ¬∑ exact isOpen_iff_of_isColimit_cofork c hc

theorem coequalizer_isOpen_iff (U : Set ((coequalizer f g :) : Type u)) :
    IsOpen U ‚Üî IsOpen (coequalizer.œÄ f g ‚Åª¬π' U) :=
  isOpen_iff_of_isColimit_cofork _ (coequalizerIsCoequalizer f g) _

end

end TopCat
