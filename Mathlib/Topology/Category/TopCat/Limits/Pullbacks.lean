/-
Copyright (c) 2017 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Scott Morrison, Mario Carneiro, Andrew Yang
-/
import Mathlib.Topology.Category.TopCat.Limits.Products

#align_import topology.category.Top.limits.pullbacks from "leanprover-community/mathlib"@"178a32653e369dce2da68dc6b2694e385d484ef1"

/-!
# Pullbacks and pushouts in the category of topological spaces
-/

-- Porting note: every ML3 decl has an uppercase letter
set_option linter.uppercaseLean3 false

open TopologicalSpace

open CategoryTheory

open CategoryTheory.Limits

universe v u w

noncomputable section

namespace TopCat

variable {J : Type v} [SmallCategory J]

section Pullback

variable {X Y Z : TopCat.{u}}

/-- The first projection from the pullback. -/
abbrev pullbackFst (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) : TopCat.of { p : X √ó Y // f p.1 = g p.2 } ‚ü∂ X :=
  ‚ü®Prod.fst ‚àò Subtype.val, by
    apply Continuous.comp <;> set_option tactic.skipAssignedInstances false in continuity‚ü©
#align Top.pullback_fst TopCat.pullbackFst

lemma pullbackFst_apply (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) (x) : pullbackFst f g x = x.1.1 := rfl

/-- The second projection from the pullback. -/
abbrev pullbackSnd (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) : TopCat.of { p : X √ó Y // f p.1 = g p.2 } ‚ü∂ Y :=
  ‚ü®Prod.snd ‚àò Subtype.val, by
    apply Continuous.comp <;> set_option tactic.skipAssignedInstances false in continuity‚ü©
#align Top.pullback_snd TopCat.pullbackSnd

lemma pullbackSnd_apply (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) (x) : pullbackSnd f g x = x.1.2 := rfl

/-- The explicit pullback cone of `X, Y` given by `{ p : X √ó Y // f p.1 = g p.2 }`. -/
def pullbackCone (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) : PullbackCone f g :=
  PullbackCone.mk (pullbackFst f g) (pullbackSnd f g)
    (by
      dsimp [pullbackFst, pullbackSnd, Function.comp_def]
      ext ‚ü®x, h‚ü©
      -- Next 2 lines were
      -- `rw [comp_apply, ContinuousMap.coe_mk, comp_apply, ContinuousMap.coe_mk]`
      -- `exact h` before leanprover/lean4#2644
      rw [comp_apply, comp_apply]
      congr!)
#align Top.pullback_cone TopCat.pullbackCone

/-- The constructed cone is a limit. -/
def pullbackConeIsLimit (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) : IsLimit (pullbackCone f g) :=
  PullbackCone.isLimitAux' _
    (by
      intro S
      constructor; swap
      ¬∑ exact
          { toFun := fun x =>
              ‚ü®‚ü®S.fst x, S.snd x‚ü©, by simpa using ConcreteCategory.congr_hom S.condition x‚ü©
            continuous_toFun := by
              apply Continuous.subtype_mk <| Continuous.prod_mk ?_ ?_
              ¬∑ exact (PullbackCone.fst S)|>.continuous_toFun
              ¬∑ exact (PullbackCone.snd S)|>.continuous_toFun
          }
      refine ‚ü®?_, ?_, ?_‚ü©
      ¬∑ delta pullbackCone
        ext a
        -- This used to be `rw`, but we need `erw` after leanprover/lean4#2644
        erw [comp_apply, ContinuousMap.coe_mk]
      ¬∑ delta pullbackCone
        ext a
        -- This used to be `rw`, but we need `erw` after leanprover/lean4#2644
        erw [comp_apply, ContinuousMap.coe_mk]
      ¬∑ intro m h‚ÇÅ h‚ÇÇ
        -- Porting note: used to be ext x
        apply ContinuousMap.ext; intro x
        apply Subtype.ext
        apply Prod.ext
        ¬∑ simpa using ConcreteCategory.congr_hom h‚ÇÅ x
        ¬∑ simpa using ConcreteCategory.congr_hom h‚ÇÇ x)
#align Top.pullback_cone_is_limit TopCat.pullbackConeIsLimit

/-- The pullback of two maps can be identified as a subspace of `X √ó Y`. -/
def pullbackIsoProdSubtype (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :
    pullback f g ‚âÖ TopCat.of { p : X √ó Y // f p.1 = g p.2 } :=
  (limit.isLimit _).conePointUniqueUpToIso (pullbackConeIsLimit f g)
#align Top.pullback_iso_prod_subtype TopCat.pullbackIsoProdSubtype

@[reassoc (attr := simp)]
theorem pullbackIsoProdSubtype_inv_fst (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :
    (pullbackIsoProdSubtype f g).inv ‚â´ pullback.fst = pullbackFst f g := by
  simp [pullbackCone, pullbackIsoProdSubtype]
#align Top.pullback_iso_prod_subtype_inv_fst TopCat.pullbackIsoProdSubtype_inv_fst

theorem pullbackIsoProdSubtype_inv_fst_apply (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z)
    (x : { p : X √ó Y // f p.1 = g p.2 }) :
    (pullback.fst : pullback f g ‚ü∂ _) ((pullbackIsoProdSubtype f g).inv x) = (x : X √ó Y).fst :=
  ConcreteCategory.congr_hom (pullbackIsoProdSubtype_inv_fst f g) x
#align Top.pullback_iso_prod_subtype_inv_fst_apply TopCat.pullbackIsoProdSubtype_inv_fst_apply

@[reassoc (attr := simp)]
theorem pullbackIsoProdSubtype_inv_snd (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :
    (pullbackIsoProdSubtype f g).inv ‚â´ pullback.snd = pullbackSnd f g := by
  simp [pullbackCone, pullbackIsoProdSubtype]
#align Top.pullback_iso_prod_subtype_inv_snd TopCat.pullbackIsoProdSubtype_inv_snd

theorem pullbackIsoProdSubtype_inv_snd_apply (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z)
    (x : { p : X √ó Y // f p.1 = g p.2 }) :
    (pullback.snd : pullback f g ‚ü∂ _) ((pullbackIsoProdSubtype f g).inv x) = (x : X √ó Y).snd :=
  ConcreteCategory.congr_hom (pullbackIsoProdSubtype_inv_snd f g) x
#align Top.pullback_iso_prod_subtype_inv_snd_apply TopCat.pullbackIsoProdSubtype_inv_snd_apply

theorem pullbackIsoProdSubtype_hom_fst (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :
    (pullbackIsoProdSubtype f g).hom ‚â´ pullbackFst f g = pullback.fst := by
  rw [‚Üê Iso.eq_inv_comp, pullbackIsoProdSubtype_inv_fst]
#align Top.pullback_iso_prod_subtype_hom_fst TopCat.pullbackIsoProdSubtype_hom_fst

theorem pullbackIsoProdSubtype_hom_snd (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :
    (pullbackIsoProdSubtype f g).hom ‚â´ pullbackSnd f g = pullback.snd := by
  rw [‚Üê Iso.eq_inv_comp, pullbackIsoProdSubtype_inv_snd]
#align Top.pullback_iso_prod_subtype_hom_snd TopCat.pullbackIsoProdSubtype_hom_snd

-- Porting note: why do I need to tell Lean to coerce pullback to a type
theorem pullbackIsoProdSubtype_hom_apply {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z}
    (x : ConcreteCategory.forget.obj (pullback f g)) :
    (pullbackIsoProdSubtype f g).hom x =
      ‚ü®‚ü®(pullback.fst : pullback f g ‚ü∂ _) x, (pullback.snd : pullback f g ‚ü∂ _) x‚ü©, by
        simpa using ConcreteCategory.congr_hom pullback.condition x‚ü© := by
  apply Subtype.ext; apply Prod.ext
  exacts [ConcreteCategory.congr_hom (pullbackIsoProdSubtype_hom_fst f g) x,
    ConcreteCategory.congr_hom (pullbackIsoProdSubtype_hom_snd f g) x]
#align Top.pullback_iso_prod_subtype_hom_apply TopCat.pullbackIsoProdSubtype_hom_apply

theorem pullback_topology {X Y Z : TopCat.{u}} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :
    (pullback f g).str =
      induced (pullback.fst : pullback f g ‚ü∂ _) X.str ‚äì
        induced (pullback.snd : pullback f g ‚ü∂ _) Y.str := by
  let homeo := homeoOfIso (pullbackIsoProdSubtype f g)
  refine homeo.inducing.induced.trans ?_
  change induced homeo (induced _ ( (induced Prod.fst X.str) ‚äì (induced Prod.snd Y.str))) = _
  simp only [induced_compose, induced_inf]
  congr
#align Top.pullback_topology TopCat.pullback_topology

theorem range_pullback_to_prod {X Y Z : TopCat} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :
    Set.range (prod.lift pullback.fst pullback.snd : pullback f g ‚ü∂ X ‚®Ø Y) =
      { x | (Limits.prod.fst ‚â´ f) x = (Limits.prod.snd ‚â´ g) x } := by
  ext x
  constructor
  ¬∑ rintro ‚ü®y, rfl‚ü©
    change (_ ‚â´ _ ‚â´ f) _ = (_ ‚â´ _ ‚â´ g) _ -- new `change` after #13170
    simp [pullback.condition]
  ¬∑ rintro (h : f (_, _).1 = g (_, _).2)
    use (pullbackIsoProdSubtype f g).inv ‚ü®‚ü®_, _‚ü©, h‚ü©
    change (forget TopCat).map _ _ = _ -- new `change` after #13170
    apply Concrete.limit_ext
    rintro ‚ü®‚ü®‚ü©‚ü© <;>
    erw [‚Üê comp_apply, ‚Üê comp_apply, limit.lift_œÄ] <;> -- now `erw` after #13170
    -- This used to be `simp` before leanprover/lean4#2644
    aesop_cat
#align Top.range_pullback_to_prod TopCat.range_pullback_to_prod

/-- The pullback along an embedding is (isomorphic to) the preimage. -/
noncomputable
def pullbackHomeoPreimage
    {X Y Z : Type*} [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]
    (f : X ‚Üí Z) (hf : Continuous f) (g : Y ‚Üí Z) (hg : Embedding g) :
    { p : X √ó Y // f p.1 = g p.2 } ‚âÉ‚Çú f ‚Åª¬π' Set.range g where
  toFun := fun x ‚Ü¶ ‚ü®x.1.1, _, x.2.symm‚ü©
  invFun := fun x ‚Ü¶ ‚ü®‚ü®x.1, Exists.choose x.2‚ü©, (Exists.choose_spec x.2).symm‚ü©
  left_inv := by
    intro x
    ext <;> dsimp
    apply hg.inj
    convert x.prop
    exact Exists.choose_spec (p := fun y ‚Ü¶ g y = f (‚Üëx : X √ó Y).1) _
  right_inv := fun x ‚Ü¶ rfl
  continuous_toFun := by
    apply Continuous.subtype_mk
    exact continuous_fst.comp continuous_subtype_val
  continuous_invFun := by
    apply Continuous.subtype_mk
    refine continuous_prod_mk.mpr ‚ü®continuous_subtype_val, hg.toInducing.continuous_iff.mpr ?_‚ü©
    convert hf.comp continuous_subtype_val
    ext x
    exact Exists.choose_spec x.2

theorem inducing_pullback_to_prod {X Y Z : TopCat.{u}} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :
    Inducing <| ‚áë(prod.lift pullback.fst pullback.snd : pullback f g ‚ü∂ X ‚®Ø Y) :=
  ‚ü®by simp [topologicalSpace_coe, prod_topology, pullback_topology, induced_compose, ‚Üê coe_comp]‚ü©
#align Top.inducing_pullback_to_prod TopCat.inducing_pullback_to_prod

theorem embedding_pullback_to_prod {X Y Z : TopCat.{u}} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) :
    Embedding <| ‚áë(prod.lift pullback.fst pullback.snd : pullback f g ‚ü∂ X ‚®Ø Y) :=
  ‚ü®inducing_pullback_to_prod f g, (TopCat.mono_iff_injective _).mp inferInstance‚ü©
#align Top.embedding_pullback_to_prod TopCat.embedding_pullback_to_prod

/-- If the map `S ‚ü∂ T` is mono, then there is a description of the image of `W √ó‚Çõ X ‚ü∂ Y √ó‚Çú Z`. -/
theorem range_pullback_map {W X Y Z S T : TopCat} (f‚ÇÅ : W ‚ü∂ S) (f‚ÇÇ : X ‚ü∂ S) (g‚ÇÅ : Y ‚ü∂ T)
    (g‚ÇÇ : Z ‚ü∂ T) (i‚ÇÅ : W ‚ü∂ Y) (i‚ÇÇ : X ‚ü∂ Z) (i‚ÇÉ : S ‚ü∂ T) [H‚ÇÉ : Mono i‚ÇÉ] (eq‚ÇÅ : f‚ÇÅ ‚â´ i‚ÇÉ = i‚ÇÅ ‚â´ g‚ÇÅ)
    (eq‚ÇÇ : f‚ÇÇ ‚â´ i‚ÇÉ = i‚ÇÇ ‚â´ g‚ÇÇ) :
    Set.range (pullback.map f‚ÇÅ f‚ÇÇ g‚ÇÅ g‚ÇÇ i‚ÇÅ i‚ÇÇ i‚ÇÉ eq‚ÇÅ eq‚ÇÇ) =
      (pullback.fst : pullback g‚ÇÅ g‚ÇÇ ‚ü∂ _) ‚Åª¬π' Set.range i‚ÇÅ ‚à©
        (pullback.snd : pullback g‚ÇÅ g‚ÇÇ ‚ü∂ _) ‚Åª¬π' Set.range i‚ÇÇ := by
  ext
  constructor
  ¬∑ rintro ‚ü®y, rfl‚ü©
    simp only [Set.mem_inter_iff, Set.mem_preimage, Set.mem_range]
    erw [‚Üê comp_apply, ‚Üê comp_apply] -- now `erw` after #13170
    simp only [limit.lift_œÄ, PullbackCone.mk_pt, PullbackCone.mk_œÄ_app, comp_apply]
    exact ‚ü®exists_apply_eq_apply _ _, exists_apply_eq_apply _ _‚ü©
  rintro ‚ü®‚ü®x‚ÇÅ, hx‚ÇÅ‚ü©, ‚ü®x‚ÇÇ, hx‚ÇÇ‚ü©‚ü©
  have : f‚ÇÅ x‚ÇÅ = f‚ÇÇ x‚ÇÇ := by
    apply (TopCat.mono_iff_injective _).mp H‚ÇÉ
    erw [‚Üê comp_apply, eq‚ÇÅ, ‚Üê comp_apply, eq‚ÇÇ, -- now `erw` after #13170
      comp_apply, comp_apply, hx‚ÇÅ, hx‚ÇÇ, ‚Üê comp_apply, pullback.condition]
    rfl -- `rfl` was not needed before #13170
  use (pullbackIsoProdSubtype f‚ÇÅ f‚ÇÇ).inv ‚ü®‚ü®x‚ÇÅ, x‚ÇÇ‚ü©, this‚ü©
  change (forget TopCat).map _ _ = _
  apply Concrete.limit_ext
  rintro (_ | _ | _) <;>
  erw [‚Üê comp_apply, ‚Üê comp_apply] -- now `erw` after #13170
  simp only [Category.assoc, limit.lift_œÄ, PullbackCone.mk_œÄ_app_one]
  ¬∑ simp only [cospan_one, pullbackIsoProdSubtype_inv_fst_assoc, comp_apply]
    erw [pullbackFst_apply, hx‚ÇÅ]
    rw [‚Üê limit.w _ WalkingCospan.Hom.inl, cospan_map_inl, comp_apply (g := g‚ÇÅ)]
    rfl -- `rfl` was not needed before #13170
  ¬∑ simp only [cospan_left, limit.lift_œÄ, PullbackCone.mk_pt, PullbackCone.mk_œÄ_app,
      pullbackIsoProdSubtype_inv_fst_assoc, comp_apply]
    erw [hx‚ÇÅ] -- now `erw` after #13170
    rfl -- `rfl` was not needed before #13170
  ¬∑ simp only [cospan_right, limit.lift_œÄ, PullbackCone.mk_pt, PullbackCone.mk_œÄ_app,
      pullbackIsoProdSubtype_inv_snd_assoc, comp_apply]
    erw [hx‚ÇÇ] -- now `erw` after #13170
    rfl -- `rfl` was not needed before #13170
#align Top.range_pullback_map TopCat.range_pullback_map

theorem pullback_fst_range {X Y S : TopCat} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) :
    Set.range (pullback.fst : pullback f g ‚ü∂ _) = { x : X | ‚àÉ y : Y, f x = g y } := by
  ext x
  constructor
  ¬∑ rintro ‚ü®(y : (forget TopCat).obj _), rfl‚ü©
    use (pullback.snd : pullback f g ‚ü∂ _) y
    exact ConcreteCategory.congr_hom pullback.condition y
  ¬∑ rintro ‚ü®y, eq‚ü©
    use (TopCat.pullbackIsoProdSubtype f g).inv ‚ü®‚ü®x, y‚ü©, eq‚ü©
    rw [pullbackIsoProdSubtype_inv_fst_apply]
#align Top.pullback_fst_range TopCat.pullback_fst_range

theorem pullback_snd_range {X Y S : TopCat} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) :
    Set.range (pullback.snd : pullback f g ‚ü∂ _) = { y : Y | ‚àÉ x : X, f x = g y } := by
  ext y
  constructor
  ¬∑ rintro ‚ü®(x : (forget TopCat).obj _), rfl‚ü©
    use (pullback.fst : pullback f g ‚ü∂ _) x
    exact ConcreteCategory.congr_hom pullback.condition x
  ¬∑ rintro ‚ü®x, eq‚ü©
    use (TopCat.pullbackIsoProdSubtype f g).inv ‚ü®‚ü®x, y‚ü©, eq‚ü©
    rw [pullbackIsoProdSubtype_inv_snd_apply]
#align Top.pullback_snd_range TopCat.pullback_snd_range

/-- If there is a diagram where the morphisms `W ‚ü∂ Y` and `X ‚ü∂ Z` are embeddings,
then the induced morphism `W √ó‚Çõ X ‚ü∂ Y √ó‚Çú Z` is also an embedding.

  W ‚ü∂ Y
    ‚Üò      ‚Üò
      S ‚ü∂ T
    ‚Üó      ‚Üó
  X ‚ü∂ Z
-/
theorem pullback_map_embedding_of_embeddings {W X Y Z S T : TopCat.{u}} (f‚ÇÅ : W ‚ü∂ S) (f‚ÇÇ : X ‚ü∂ S)
    (g‚ÇÅ : Y ‚ü∂ T) (g‚ÇÇ : Z ‚ü∂ T) {i‚ÇÅ : W ‚ü∂ Y} {i‚ÇÇ : X ‚ü∂ Z} (H‚ÇÅ : Embedding i‚ÇÅ) (H‚ÇÇ : Embedding i‚ÇÇ)
    (i‚ÇÉ : S ‚ü∂ T) (eq‚ÇÅ : f‚ÇÅ ‚â´ i‚ÇÉ = i‚ÇÅ ‚â´ g‚ÇÅ) (eq‚ÇÇ : f‚ÇÇ ‚â´ i‚ÇÉ = i‚ÇÇ ‚â´ g‚ÇÇ) :
    Embedding (pullback.map f‚ÇÅ f‚ÇÇ g‚ÇÅ g‚ÇÇ i‚ÇÅ i‚ÇÇ i‚ÇÉ eq‚ÇÅ eq‚ÇÇ) := by
  refine
    embedding_of_embedding_compose (ContinuousMap.continuous_toFun _)
      (show Continuous (prod.lift pullback.fst pullback.snd : pullback g‚ÇÅ g‚ÇÇ ‚ü∂ Y ‚®Ø Z) from
        ContinuousMap.continuous_toFun _)
      ?_
  suffices
    Embedding (prod.lift pullback.fst pullback.snd ‚â´ Limits.prod.map i‚ÇÅ i‚ÇÇ : pullback f‚ÇÅ f‚ÇÇ ‚ü∂ _) by
    simpa [‚Üê coe_comp] using this
  rw [coe_comp]
  exact Embedding.comp (embedding_prod_map H‚ÇÅ H‚ÇÇ) (embedding_pullback_to_prod _ _)
#align Top.pullback_map_embedding_of_embeddings TopCat.pullback_map_embedding_of_embeddings

/-- If there is a diagram where the morphisms `W ‚ü∂ Y` and `X ‚ü∂ Z` are open embeddings, and `S ‚ü∂ T`
is mono, then the induced morphism `W √ó‚Çõ X ‚ü∂ Y √ó‚Çú Z` is also an open embedding.
  W ‚ü∂ Y
    ‚Üò      ‚Üò
      S ‚ü∂ T
    ‚Üó       ‚Üó
  X ‚ü∂ Z
-/
theorem pullback_map_openEmbedding_of_open_embeddings {W X Y Z S T : TopCat.{u}} (f‚ÇÅ : W ‚ü∂ S)
    (f‚ÇÇ : X ‚ü∂ S) (g‚ÇÅ : Y ‚ü∂ T) (g‚ÇÇ : Z ‚ü∂ T) {i‚ÇÅ : W ‚ü∂ Y} {i‚ÇÇ : X ‚ü∂ Z} (H‚ÇÅ : OpenEmbedding i‚ÇÅ)
    (H‚ÇÇ : OpenEmbedding i‚ÇÇ) (i‚ÇÉ : S ‚ü∂ T) [H‚ÇÉ : Mono i‚ÇÉ] (eq‚ÇÅ : f‚ÇÅ ‚â´ i‚ÇÉ = i‚ÇÅ ‚â´ g‚ÇÅ)
    (eq‚ÇÇ : f‚ÇÇ ‚â´ i‚ÇÉ = i‚ÇÇ ‚â´ g‚ÇÇ) : OpenEmbedding (pullback.map f‚ÇÅ f‚ÇÇ g‚ÇÅ g‚ÇÇ i‚ÇÅ i‚ÇÇ i‚ÇÉ eq‚ÇÅ eq‚ÇÇ) := by
  constructor
  ¬∑ apply
      pullback_map_embedding_of_embeddings f‚ÇÅ f‚ÇÇ g‚ÇÅ g‚ÇÇ H‚ÇÅ.toEmbedding H‚ÇÇ.toEmbedding i‚ÇÉ eq‚ÇÅ eq‚ÇÇ
  ¬∑ rw [range_pullback_map]
    apply IsOpen.inter <;> apply Continuous.isOpen_preimage
    ¬∑ apply ContinuousMap.continuous_toFun
    ¬∑ exact H‚ÇÅ.isOpen_range
    ¬∑ apply ContinuousMap.continuous_toFun
    ¬∑ exact H‚ÇÇ.isOpen_range
#align Top.pullback_map_open_embedding_of_open_embeddings TopCat.pullback_map_openEmbedding_of_open_embeddings

theorem snd_embedding_of_left_embedding {X Y S : TopCat} {f : X ‚ü∂ S} (H : Embedding f) (g : Y ‚ü∂ S) :
    Embedding <| ‚áë(pullback.snd : pullback f g ‚ü∂ Y) := by
  convert (homeoOfIso (asIso (pullback.snd : pullback (ùüô S) g ‚ü∂ _))).embedding.comp
      (pullback_map_embedding_of_embeddings (i‚ÇÇ := ùüô Y)
        f g (ùüô S) g H (homeoOfIso (Iso.refl _)).embedding (ùüô _) rfl (by simp))
  erw [‚Üê coe_comp]
  simp
#align Top.snd_embedding_of_left_embedding TopCat.snd_embedding_of_left_embedding

theorem fst_embedding_of_right_embedding {X Y S : TopCat} (f : X ‚ü∂ S) {g : Y ‚ü∂ S}
    (H : Embedding g) : Embedding <| ‚áë(pullback.fst : pullback f g ‚ü∂ X) := by
  convert (homeoOfIso (asIso (pullback.fst : pullback f (ùüô S) ‚ü∂ _))).embedding.comp
      (pullback_map_embedding_of_embeddings (i‚ÇÅ := ùüô X)
        f g f (ùüô _) (homeoOfIso (Iso.refl _)).embedding H (ùüô _) rfl (by simp))
  erw [‚Üê coe_comp]
  simp
#align Top.fst_embedding_of_right_embedding TopCat.fst_embedding_of_right_embedding

theorem embedding_of_pullback_embeddings {X Y S : TopCat} {f : X ‚ü∂ S} {g : Y ‚ü∂ S} (H‚ÇÅ : Embedding f)
    (H‚ÇÇ : Embedding g) : Embedding (limit.œÄ (cospan f g) WalkingCospan.one) := by
  convert H‚ÇÇ.comp (snd_embedding_of_left_embedding H‚ÇÅ g)
  erw [‚Üê coe_comp]
  rw [‚Üê limit.w _ WalkingCospan.Hom.inr]
  rfl
#align Top.embedding_of_pullback_embeddings TopCat.embedding_of_pullback_embeddings

theorem snd_openEmbedding_of_left_openEmbedding {X Y S : TopCat} {f : X ‚ü∂ S} (H : OpenEmbedding f)
    (g : Y ‚ü∂ S) : OpenEmbedding <| ‚áë(pullback.snd : pullback f g ‚ü∂ Y) := by
  convert (homeoOfIso (asIso (pullback.snd : pullback (ùüô S) g ‚ü∂ _))).openEmbedding.comp
      (pullback_map_openEmbedding_of_open_embeddings (i‚ÇÇ := ùüô Y) f g (ùüô _) g H
        (homeoOfIso (Iso.refl _)).openEmbedding (ùüô _) rfl (by simp))
  erw [‚Üê coe_comp]
  simp
#align Top.snd_open_embedding_of_left_open_embedding TopCat.snd_openEmbedding_of_left_openEmbedding

theorem fst_openEmbedding_of_right_openEmbedding {X Y S : TopCat} (f : X ‚ü∂ S) {g : Y ‚ü∂ S}
    (H : OpenEmbedding g) : OpenEmbedding <| ‚áë(pullback.fst : pullback f g ‚ü∂ X) := by
  convert (homeoOfIso (asIso (pullback.fst : pullback f (ùüô S) ‚ü∂ _))).openEmbedding.comp
      (pullback_map_openEmbedding_of_open_embeddings (i‚ÇÅ := ùüô X) f g f (ùüô _)
        (homeoOfIso (Iso.refl _)).openEmbedding H (ùüô _) rfl (by simp))
  erw [‚Üê coe_comp]
  simp
#align Top.fst_open_embedding_of_right_open_embedding TopCat.fst_openEmbedding_of_right_openEmbedding

/-- If `X ‚ü∂ S`, `Y ‚ü∂ S` are open embeddings, then so is `X √ó‚Çõ Y ‚ü∂ S`. -/
theorem openEmbedding_of_pullback_open_embeddings {X Y S : TopCat} {f : X ‚ü∂ S} {g : Y ‚ü∂ S}
    (H‚ÇÅ : OpenEmbedding f) (H‚ÇÇ : OpenEmbedding g) :
    OpenEmbedding (limit.œÄ (cospan f g) WalkingCospan.one) := by
  convert H‚ÇÇ.comp (snd_openEmbedding_of_left_openEmbedding H‚ÇÅ g)
  erw [‚Üê coe_comp]
  rw [‚Üê limit.w _ WalkingCospan.Hom.inr]
  rfl
#align Top.open_embedding_of_pullback_open_embeddings TopCat.openEmbedding_of_pullback_open_embeddings

theorem fst_iso_of_right_embedding_range_subset {X Y S : TopCat} (f : X ‚ü∂ S) {g : Y ‚ü∂ S}
    (hg : Embedding g) (H : Set.range f ‚äÜ Set.range g) :
    IsIso (pullback.fst : pullback f g ‚ü∂ X) := by
  let esto : (pullback f g : TopCat) ‚âÉ‚Çú X :=
    (Homeomorph.ofEmbedding _ (fst_embedding_of_right_embedding f hg)).trans
      { toFun := Subtype.val
        invFun := fun x =>
          ‚ü®x, by
            rw [pullback_fst_range]
            exact ‚ü®_, (H (Set.mem_range_self x)).choose_spec.symm‚ü©‚ü©
        left_inv := fun ‚ü®_, _‚ü© => rfl
        right_inv := fun x => rfl }
  convert (isoOfHomeo esto).isIso_hom
#align Top.fst_iso_of_right_embedding_range_subset TopCat.fst_iso_of_right_embedding_range_subset

theorem snd_iso_of_left_embedding_range_subset {X Y S : TopCat} {f : X ‚ü∂ S} (hf : Embedding f)
    (g : Y ‚ü∂ S) (H : Set.range g ‚äÜ Set.range f) : IsIso (pullback.snd : pullback f g ‚ü∂ Y) := by
  let esto : (pullback f g : TopCat) ‚âÉ‚Çú Y :=
    (Homeomorph.ofEmbedding _ (snd_embedding_of_left_embedding hf g)).trans
      { toFun := Subtype.val
        invFun := fun x =>
          ‚ü®x, by
            rw [pullback_snd_range]
            exact ‚ü®_, (H (Set.mem_range_self x)).choose_spec‚ü©‚ü©
        left_inv := fun ‚ü®_, _‚ü© => rfl
        right_inv := fun x => rfl }
  convert (isoOfHomeo esto).isIso_hom
#align Top.snd_iso_of_left_embedding_range_subset TopCat.snd_iso_of_left_embedding_range_subset

theorem pullback_snd_image_fst_preimage (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) (U : Set X) :
    (pullback.snd : pullback f g ‚ü∂ _) '' ((pullback.fst : pullback f g ‚ü∂ _) ‚Åª¬π' U) =
      g ‚Åª¬π' (f '' U) := by
  ext x
  constructor
  ¬∑ rintro ‚ü®(y : (forget TopCat).obj _), hy, rfl‚ü©
    exact
      ‚ü®(pullback.fst : pullback f g ‚ü∂ _) y, hy, ConcreteCategory.congr_hom pullback.condition y‚ü©
  ¬∑ rintro ‚ü®y, hy, eq‚ü©
  -- next 5 lines were
  -- `exact ‚ü®(TopCat.pullbackIsoProdSubtype f g).inv ‚ü®‚ü®_, _‚ü©, eq‚ü©, by simpa, by simp‚ü©` before #13170
    refine ‚ü®(TopCat.pullbackIsoProdSubtype f g).inv ‚ü®‚ü®_, _‚ü©, eq‚ü©, ?_, ?_‚ü©
    ¬∑ simp only [coe_of, Set.mem_preimage]
      convert hy
      erw [pullbackIsoProdSubtype_inv_fst_apply]
    ¬∑ rw [pullbackIsoProdSubtype_inv_snd_apply]
#align Top.pullback_snd_image_fst_preimage TopCat.pullback_snd_image_fst_preimage

theorem pullback_fst_image_snd_preimage (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) (U : Set Y) :
    (pullback.fst : pullback f g ‚ü∂ _) '' ((pullback.snd : pullback f g ‚ü∂ _) ‚Åª¬π' U) =
      f ‚Åª¬π' (g '' U) := by
  ext x
  constructor
  ¬∑ rintro ‚ü®(y : (forget TopCat).obj _), hy, rfl‚ü©
    exact
      ‚ü®(pullback.snd : pullback f g ‚ü∂ _) y, hy,
        (ConcreteCategory.congr_hom pullback.condition y).symm‚ü©
  ¬∑ rintro ‚ü®y, hy, eq‚ü©
    -- next 5 lines were
    -- `exact ‚ü®(TopCat.pullbackIsoProdSubtype f g).inv ‚ü®‚ü®_, _‚ü©, eq.symm‚ü©, by simpa, by simp‚ü©`
    -- before #13170
    refine ‚ü®(TopCat.pullbackIsoProdSubtype f g).inv ‚ü®‚ü®_, _‚ü©, eq.symm‚ü©, ?_, ?_‚ü©
    ¬∑ simp only [coe_of, Set.mem_preimage]
      convert hy
      erw [pullbackIsoProdSubtype_inv_snd_apply]
    ¬∑ rw [pullbackIsoProdSubtype_inv_fst_apply]
#align Top.pullback_fst_image_snd_preimage TopCat.pullback_fst_image_snd_preimage

end Pullback

--TODO: Add analogous constructions for `pushout`.
theorem coinduced_of_isColimit {F : J ‚•§ TopCat.{max v u}} (c : Cocone F) (hc : IsColimit c) :
    c.pt.str = ‚®Ü j, (F.obj j).str.coinduced (c.Œπ.app j) := by
  let homeo := homeoOfIso (hc.coconePointUniqueUpToIso (colimitCoconeIsColimit F))
  ext
  refine homeo.symm.isOpen_preimage.symm.trans (Iff.trans ?_ isOpen_iSup_iff.symm)
  exact isOpen_iSup_iff
#align Top.coinduced_of_is_colimit TopCat.coinduced_of_isColimit

theorem colimit_topology (F : J ‚•§ TopCat.{max v u}) :
    (colimit F).str = ‚®Ü j, (F.obj j).str.coinduced (colimit.Œπ F j) :=
  coinduced_of_isColimit _ (colimit.isColimit F)
#align Top.colimit_topology TopCat.colimit_topology

theorem colimit_isOpen_iff (F : J ‚•§ TopCat.{max v u}) (U : Set ((colimit F : _) : Type max v u)) :
    IsOpen U ‚Üî ‚àÄ j, IsOpen (colimit.Œπ F j ‚Åª¬π' U) := by
  dsimp [topologicalSpace_coe]
  conv_lhs => rw [colimit_topology F]
  exact isOpen_iSup_iff
#align Top.colimit_is_open_iff TopCat.colimit_isOpen_iff

theorem coequalizer_isOpen_iff (F : WalkingParallelPair ‚•§ TopCat.{u})
    (U : Set ((colimit F : _) : Type u)) :
    IsOpen U ‚Üî IsOpen (colimit.Œπ F WalkingParallelPair.one ‚Åª¬π' U) := by
  rw [colimit_isOpen_iff]
  constructor
  ¬∑ intro H
    exact H _
  ¬∑ intro H j
    cases j
    ¬∑ rw [‚Üê colimit.w F WalkingParallelPairHom.left]
      exact (F.map WalkingParallelPairHom.left).continuous_toFun.isOpen_preimage _ H
    ¬∑ exact H
#align Top.coequalizer_is_open_iff TopCat.coequalizer_isOpen_iff

end TopCat
