/-
Copyright (c) 2024 Elliot Dean Young and Jiazhen Xia. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jiazhen Xia, Elliot Dean Young
-/

import Mathlib.Analysis.InnerProductSpace.PiL2
import Mathlib.Topology.Category.TopCat.EpiMono

/-!
# Euclidean spheres

This file defines the `n`-sphere `ğ•Š n`, the `n`-disk `ğ”» n`, its boundary `âˆ‚ğ”» n` and its interior
`ğ”¹ n` as objects in `TopCat`.

-/

universe u

namespace TopCat
open CategoryTheory

/-- The `n`-disk is the set of points in â„â¿ whose norm is at most `1`,
endowed with the subspace topology. -/
noncomputable def disk (n : â„•) : TopCat.{u} :=
  TopCat.of <| ULift <| Metric.closedBall (0 : EuclideanSpace â„ (Fin n)) 1

/-- The boundary of the `n`-disk. -/
noncomputable def diskBoundary (n : â„•) : TopCat.{u} :=
  TopCat.of <| ULift <| Metric.sphere (0 : EuclideanSpace â„ (Fin n)) 1

/-- The `n`-sphere is the set of points in â„â¿âºÂ¹ whose norm equals `1`,
endowed with the subspace topology. -/
noncomputable def sphere (n : â„•) : TopCat.{u} :=
  diskBoundary (n + 1)

/-- The `n`-ball is the set of points in â„â¿ whose norm is strictly less than `1`,
endowed with the subspace topology. -/
noncomputable def ball (n : â„•) : TopCat.{u} :=
  TopCat.of <| ULift <| Metric.ball (0 : EuclideanSpace â„ (Fin n)) 1

/-- `ğ”» n` denotes the `n`-disk. -/
scoped prefix:arg "ğ”» " => disk

/-- `âˆ‚ğ”» n` denotes the boundary of the `n`-disk. -/
scoped prefix:arg "âˆ‚ğ”» " => diskBoundary

/-- `ğ•Š n` denotes the `n`-sphere. -/
scoped prefix:arg "ğ•Š " => sphere

/-- `ğ”¹ n` denotes the `n`-ball, the interior of the `n`-disk. -/
scoped prefix:arg "ğ”¹ " => ball

/-- The inclusion `âˆ‚ğ”» n âŸ¶ ğ”» n` of the boundary of the `n`-disk. -/
def diskBoundaryInclusion (n : â„•) : âˆ‚ğ”» n âŸ¶ ğ”» n :=
  ofHom
    { toFun := fun âŸ¨p, hpâŸ© â†¦ âŸ¨p, le_of_eq hpâŸ©
      continuous_toFun := âŸ¨fun t âŸ¨s, âŸ¨r, hro, hrsâŸ©, hstâŸ© â†¦ by
        rw [isOpen_induced_iff, â† hst, â† hrs]
        tautoâŸ© }

/-- The inclusion `ğ”¹ n âŸ¶ ğ”» n` of the interior of the `n`-disk. -/
def ballInclusion (n : â„•) : ğ”¹ n âŸ¶ ğ”» n :=
  ofHom
    { toFun := fun âŸ¨p, hpâŸ© â†¦ âŸ¨p, Metric.ball_subset_closedBall hpâŸ©
      continuous_toFun := âŸ¨fun t âŸ¨s, âŸ¨r, hro, hrsâŸ©, hstâŸ© â†¦ by
        rw [isOpen_induced_iff, â† hst, â† hrs]
        tautoâŸ© }

instance {n : â„•} : Mono (diskBoundaryInclusion n) := mono_iff_injective _ |>.mpr <| by
  intro âŸ¨x, hxâŸ© âŸ¨y, hyâŸ© h
  obtain rfl : x = y := by simpa [diskBoundaryInclusion, disk] using h
  congr

instance {n : â„•} : Mono (ballInclusion n) := TopCat.mono_iff_injective _ |>.mpr <| by
  intro âŸ¨x, hxâŸ© âŸ¨y, hyâŸ© h
  obtain rfl : x = y := by simpa [ballInclusion, disk] using h
  congr

instance (n : â„•) : CompactSpace (ğ”» n) := by
  convert Homeomorph.compactSpace Homeomorph.ulift.symm
  infer_instance

instance (n : â„•) : CompactSpace (âˆ‚ğ”» n) := by
  convert Homeomorph.compactSpace Homeomorph.ulift.symm
  infer_instance

end TopCat
