/-
Copyright (c) 2019 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.Topology.Category.TopCat.Opens
import Mathlib.Data.Set.Subsingleton

/-!
# The category of open neighborhoods of a point

Given an object `X` of the category `TopCat` of topological spaces and a point `x : X`, this file
builds the type `OpenNhds x` of open neighborhoods of `x` in `X` and endows it with the partial
order given by inclusion and the corresponding category structure (as a full subcategory of the
poset category `Set X`). This is used in `Topology.Sheaves.Stalks` to build the stalk of a sheaf
at `x` as a limit over `OpenNhds x`.

## Main declarations

Besides `OpenNhds`, the main constructions here are:

* `inclusion (x : X)`: the obvious functor `OpenNhds x ‚•§ Opens X`
* `functorNhds`: An open map `f : X ‚ü∂ Y` induces a functor `OpenNhds x ‚•§ OpenNhds (f x)`
* `adjunctionNhds`: An open map `f : X ‚ü∂ Y` induces an adjunction between `OpenNhds x` and
                    `OpenNhds (f x)`.
-/


open CategoryTheory TopologicalSpace Opposite Topology

universe u

variable {X Y : TopCat.{u}} (f : X ‚ü∂ Y)

namespace TopologicalSpace

/-- The type of open neighbourhoods of a point `x` in a (bundled) topological space. -/
def OpenNhds (x : X) :=
  ObjectProperty.FullSubcategory fun U : Opens X ‚Ü¶ x ‚àà U

namespace OpenNhds
variable {x : X} {U V W : OpenNhds x}

instance partialOrder (x : X) : PartialOrder (OpenNhds x) where
  le U V := U.1 ‚â§ V.1
  le_refl _ := le_rfl
  le_trans _ _ _ := le_trans
  le_antisymm _ _ i j := ObjectProperty.FullSubcategory.ext <| le_antisymm i j

instance (x : X) : Lattice (OpenNhds x) :=
  { OpenNhds.partialOrder x with
    inf := fun U V ‚Ü¶ ‚ü®U.1 ‚äì V.1, ‚ü®U.2, V.2‚ü©‚ü©
    le_inf := fun U V W ‚Ü¶ @le_inf _ _ U.1.1 V.1.1 W.1.1
    inf_le_left := fun U V ‚Ü¶ @inf_le_left _ _ U.1.1 V.1.1
    inf_le_right := fun U V ‚Ü¶ @inf_le_right _ _ U.1.1 V.1.1
    sup := fun U V ‚Ü¶ ‚ü®U.1 ‚äî V.1, Set.mem_union_left V.1.1 U.2‚ü©
    sup_le := fun U V W ‚Ü¶ @sup_le _ _ U.1.1 V.1.1 W.1.1
    le_sup_left := fun U V ‚Ü¶ @le_sup_left _ _ U.1.1 V.1.1
    le_sup_right := fun U V ‚Ü¶ @le_sup_right _ _ U.1.1 V.1.1 }

instance (x : X) : OrderTop (OpenNhds x) where
  top := ‚ü®‚ä§, trivial‚ü©
  le_top _ := by dsimp [LE.le]; exact le_top

instance (x : X) : Inhabited (OpenNhds x) :=
  ‚ü®‚ä§‚ü©

instance openNhdsCategory (x : X) : Category.{u} (OpenNhds x) := inferInstance

instance opensNhds.instFunLike : FunLike (U ‚ü∂ V) U.1 V.1 where
  coe f := Set.inclusion f.le
  coe_injective' := by rintro ‚ü®‚ü®_‚ü©‚ü© _ _; congr!

@[simp] lemma apply_mk (f : U ‚ü∂ V) (y : X) (hy) : f ‚ü®y, hy‚ü© = ‚ü®y, f.le hy‚ü© := rfl

@[simp] lemma val_apply (f : U ‚ü∂ V) (y : U.1) : (f y : X) = y := rfl

@[simp, norm_cast] lemma coe_id (f : U ‚ü∂ U) : ‚áëf = id := rfl

lemma id_apply (f : U ‚ü∂ U) (y : U.1) : f y = y := rfl

@[simp] lemma comp_apply (f : U ‚ü∂ V) (g : V ‚ü∂ W) (x : U.1) : (f ‚â´ g) x = g (f x) := rfl

/-- The inclusion `U ‚äì V ‚ü∂ U` as a morphism in the category of open sets. -/
def infLELeft {x : X} (U V : OpenNhds x) : U ‚äì V ‚ü∂ U :=
  homOfLE inf_le_left

/-- The inclusion `U ‚äì V ‚ü∂ V` as a morphism in the category of open sets. -/
def infLERight {x : X} (U V : OpenNhds x) : U ‚äì V ‚ü∂ V :=
  homOfLE inf_le_right

/-- The inclusion functor from open neighbourhoods of `x`
to open sets in the ambient topological space. -/
def inclusion (x : X) : OpenNhds x ‚•§ Opens X :=
  ObjectProperty.Œπ _

@[simp]
theorem inclusion_obj (x : X) (U) (p) : (inclusion x).obj ‚ü®U, p‚ü© = U :=
  rfl

theorem isOpenEmbedding {x : X} (U : OpenNhds x) : IsOpenEmbedding U.1.inclusion' :=
  U.1.isOpenEmbedding

/-- The preimage functor from neighborhoods of `f x` to neighborhoods of `x`. -/
def map (x : X) : OpenNhds (f x) ‚•§ OpenNhds x where
  obj U := ‚ü®(Opens.map f).obj U.1, U.2‚ü©
  map i := (Opens.map f).map i

@[simp]
theorem map_obj (x : X) (U) (q) : (map f x).obj ‚ü®U, q‚ü© = ‚ü®(Opens.map f).obj U, q‚ü© :=
  rfl

@[simp]
theorem map_id_obj (x : X) (U) : (map (ùüô X) x).obj U = U := rfl

@[simp]
theorem map_id_obj' (x : X) (U) (p) (q) : (map (ùüô X) x).obj ‚ü®‚ü®U, p‚ü©, q‚ü© = ‚ü®‚ü®U, p‚ü©, q‚ü© :=
  rfl

@[simp]
theorem map_id_obj_unop (x : X) (U : (OpenNhds x)·µí·µñ) : (map (ùüô X) x).obj (unop U) = unop U := by
  simp

@[simp]
theorem op_map_id_obj (x : X) (U : (OpenNhds x)·µí·µñ) : (map (ùüô X) x).op.obj U = U := by simp

/-- `Opens.map f` and `OpenNhds.map f` form a commuting square (up to natural isomorphism)
with the inclusion functors into `Opens X`. -/
@[simps! hom_app inv_app]
def inclusionMapIso (x : X) : inclusion (f x) ‚ãô Opens.map f ‚âÖ map f x ‚ãô inclusion x :=
  NatIso.ofComponents fun U ‚Ü¶ { hom := ùüô _, inv := ùüô _ }

@[simp]
theorem inclusionMapIso_hom (x : X) : (inclusionMapIso f x).hom = ùüô _ :=
  rfl

@[simp]
theorem inclusionMapIso_inv (x : X) : (inclusionMapIso f x).inv = ùüô _ :=
  rfl

end OpenNhds

end TopologicalSpace

namespace IsOpenMap

open TopologicalSpace

variable {f}

/-- An open map `f : X ‚ü∂ Y` induces a functor `OpenNhds x ‚•§ OpenNhds (f x)`. -/
@[simps]
def functorNhds (h : IsOpenMap f) (x : X) : OpenNhds x ‚•§ OpenNhds (f x) where
  obj U := ‚ü®h.functor.obj U.1, ‚ü®x, U.2, rfl‚ü©‚ü©
  map i := h.functor.map i

/-- An open map `f : X ‚ü∂ Y` induces an adjunction between `OpenNhds x` and `OpenNhds (f x)`. -/
def adjunctionNhds (h : IsOpenMap f) (x : X) : IsOpenMap.functorNhds h x ‚ä£ OpenNhds.map f x where
  unit := { app := fun _ ‚Ü¶ homOfLE fun x hxU ‚Ü¶ ‚ü®x, hxU, rfl‚ü© }
  counit := { app := fun _ ‚Ü¶ homOfLE fun _ ‚ü®_, hfxV, hxy‚ü© ‚Ü¶ hxy ‚ñ∏ hfxV }

end IsOpenMap

namespace Topology.IsInducing

open TopologicalSpace

variable {f}

/-- An inducing map `f : X ‚ü∂ Y` induces a functor `open_nhds x ‚•§ open_nhds (f x)`. -/
@[simps]
def functorNhds (h : IsInducing f) (x : X) :
    OpenNhds x ‚•§ OpenNhds (f x) where
  obj U := ‚ü®h.functor.obj U.1, (h.mem_functorObj_iff U.1).mpr U.2‚ü©
  map := h.functor.map

/--
An inducing map `f : X ‚ü∂ Y` induces an adjunction between `open_nhds x` and `open_nhds (f x)`.
-/
def adjunctionNhds (h : IsInducing f) (x : X) :
    OpenNhds.map f x ‚ä£ h.functorNhds x where
  unit := { app := fun U ‚Ü¶ homOfLE (h.adjunction.unit.app U.1).le }
  counit := { app := fun U ‚Ü¶ homOfLE (h.adjunction.counit.app U.1).le }

end Topology.IsInducing
