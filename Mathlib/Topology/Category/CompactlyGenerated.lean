/-
Copyright (c) 2024 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson
-/
import Mathlib.Topology.Category.CompHaus.Basic
import Mathlib.CategoryTheory.Elementwise
/-!

# Compactly generated topological spaces

This file defines the category of compactly generated topological spaces. These are spaces `X`Â such
that a map `f : X â†’ Y` is continuous whenever the composition `S â†’ X â†’ Y` is continuous for all
compact Hausdorff spaces `S` mapping continuously to `X`.

## TODO

* `CompactlyGenerated` is a reflective subcategory of `TopCat`.
* `CompactlyGenerated` is cartesian closed.
* Every first-countable space is `u`-compactly generated for every universe `u`.
-/

attribute [local instance] CategoryTheory.ConcreteCategory.instFunLike

universe u w

open CategoryTheory Topology TopologicalSpace

/--
The compactly generated topology on a topological space `X`. This is the finest topology
which makes all maps from compact Hausdorff spaces to `X`, which are continuous for the original
topology, continuous.

Note: this definition should be used with an explicit universe parameter `u`Â for the size of the
compact Hausdorff spaces mapping to `X`.
-/
def TopologicalSpace.compactlyGenerated (X : Type w) [TopologicalSpace X] : TopologicalSpace X :=
  let f : (Î£ (i : (S : CompHaus.{u}) Ã— C(S, X)), i.fst) â†’ X := fun âŸ¨âŸ¨_, iâŸ©, sâŸ© â†¦ i s
  coinduced f inferInstance

lemma continuous_from_compactlyGenerated {X : Type w} [TopologicalSpace X]
    {Y : Type*} [t : TopologicalSpace Y] (f : X â†’ Y)
      (h : âˆ€ (S : CompHaus.{u}) (g : C(S, X)), Continuous (f âˆ˜ g)) :
        Continuous[compactlyGenerated.{u} X, t] f := by
  rw [continuous_coinduced_dom]
  continuity

/--
A topological space `X` is compactly generated if its topology is finer than (and thus equal to)
the compactly generated topology, i.e. it is coinduced by the continuous maps from compact
Hausdorff spaces to `X`.
-/
class CompactlyGeneratedSpace (X : Type w) [t : TopologicalSpace X] : Prop where
  /-- The topology of `X` is finer than the compactly generated topology. -/
  le_compactlyGenerated : t â‰¤ compactlyGenerated.{u} X

lemma eq_compactlyGenerated {X : Type w} [t : TopologicalSpace X] [CompactlyGeneratedSpace.{u} X] :
    t = compactlyGenerated.{u} X := by
  apply le_antisymm
  Â· exact CompactlyGeneratedSpace.le_compactlyGenerated
  Â· simp only [compactlyGenerated, â† continuous_iff_coinduced_le, continuous_sigma_iff,
      Sigma.forall]
    exact fun S f â†¦ f.2

instance (X : Type w) [t : TopologicalSpace X] [DiscreteTopology X] :
    CompactlyGeneratedSpace.{u} X where
  le_compactlyGenerated := by
    rw [DiscreteTopology.eq_bot (t := t)]
    exact bot_le

lemma continuous_from_compactlyGeneratedSpace {X : Type w} [TopologicalSpace X]
    [CompactlyGeneratedSpace.{u} X] {Y : Type*} [TopologicalSpace Y] (f : X â†’ Y)
      (h : âˆ€ (S : CompHaus.{u}) (g : C(S, X)), Continuous (f âˆ˜ g)) : Continuous f := by
  apply continuous_le_dom CompactlyGeneratedSpace.le_compactlyGenerated
  exact continuous_from_compactlyGenerated f h

lemma compactlyGeneratedSpace_of_continuous_maps {X : Type w} [t : TopologicalSpace X]
    (h : âˆ€ {Y : Type w} [tY : TopologicalSpace Y] (f : X â†’ Y),
      (âˆ€ (S : CompHaus.{u}) (g : C(S, X)), Continuous (f âˆ˜ g)) â†’ Continuous f) :
        CompactlyGeneratedSpace.{u} X where
  le_compactlyGenerated := by
    suffices Continuous[t, compactlyGenerated.{u} X] (id : X â†’ X) by
      rwa [â† continuous_id_iff_le]
    apply h (tY := compactlyGenerated.{u} X)
    intro S g
    let f : (Î£ (i : (T : CompHaus.{u}) Ã— C(T, X)), i.fst) â†’ X := fun âŸ¨âŸ¨_, iâŸ©, sâŸ© â†¦ i s
    suffices âˆ€ (i : (T : CompHaus.{u}) Ã— C(T, X)),
      Continuous[inferInstance, compactlyGenerated X] (fun (a : i.fst) â†¦ f âŸ¨i, aâŸ©) from this âŸ¨S, gâŸ©
    rw [â† @continuous_sigma_iff]
    apply continuous_coinduced_rng

theorem compactlyGeneratedSpace_iff {X : Type u} [TopologicalSpace X] :
    CompactlyGeneratedSpace.{u} X â†”
      (âˆ€ s, IsClosed s â†”
        (âˆ€ {K : Type u} [TopologicalSpace K], [CompactSpace K] â†’ [T2Space K] â†’
          âˆ€ (f : K â†’ X), Continuous f â†’ IsClosed (f â»Â¹' s))) where
  mp := by
    refine fun _ s â†¦ âŸ¨fun hs _ _ _ _ f hf â†¦ hs.preimage hf, fun h â†¦ ?_âŸ©
    rw [eq_compactlyGenerated (X := X), TopologicalSpace.compactlyGenerated, isClosed_coinduced,
      isClosed_sigma_iff]
    exact fun âŸ¨_, fâŸ© â†¦ h f f.continuous
  mpr := by
    refine fun h1 â†¦ compactlyGeneratedSpace_of_continuous_maps fun f h2 â†¦
      continuous_iff_isClosed.2 fun t ht â†¦ (h1 _).2 ?_
    intro K _ _ _ g hg
    exact ht.preimage (h2 (CompHaus.of K) { toFun := g, continuous_toFun := hg })

theorem isOpenSingleton (n : â„•) : @IsOpen ENat (Preorder.topology ENat) {â†‘n} := by
  let _ := Preorder.topology ENat
  have _ : OrderTopology ENat := OrderTopology.mk rfl
  cases n with
  | zero =>
    rw [isOpen_iff_generate_intervals]
    constructor
    use 1
    right
    ext x
    simp
    exact ENat.lt_one_iff_eq_zero.symm
  | succ k =>
    have : {@Nat.cast ENat _ (k + 1)} = (Set.Iio â†‘(k + 2)) âˆ© (Set.Ioi â†‘k) := by
      ext x
      simp only [Set.mem_singleton_iff, Set.mem_inter_iff, Set.mem_Iio, Set.mem_Ioi]
      rcases eq_or_ne x âŠ¤ with h | h
      Â· cases h
        simp only [not_top_lt, false_and, iff_false]
        apply ENat.top_ne_coe
      Â· lift x to â„• using h
        rw [Nat.cast_inj, Nat.cast_lt, Nat.cast_lt]
        omega
    rw [isOpen_iff_generate_intervals, this]
    apply GenerateOpen.inter <;> constructor
    Â· exact âŸ¨@Nat.cast ENat _ (k + 2), Or.inr rflâŸ©
    Â· exact âŸ¨k, Or.inl rflâŸ©

theorem lol (s : Set ENat) (h : âŠ¤ âˆ‰ s) : @IsOpen ENat (Preorder.topology ENat) s := by
  let _ := Preorder.topology ENat
  rw [â† Set.biUnion_of_singleton s]
  apply isOpen_biUnion
  intro x hx
  have : x â‰  âŠ¤ := ne_of_mem_of_not_mem hx h
  lift x to â„• using this
  exact isOpenSingleton x

theorem lol' (s : Set ENat) (h : âŠ¤ âˆˆ s) :
    @IsOpen ENat (Preorder.topology ENat) s â†” âˆƒ x : â„•, Set.Ioi â†‘x âŠ† s where
  mp hs := by
    induction hs with
    | basic t ht =>
      rcases ht with âŸ¨a, h' | h'âŸ©
      Â· rw [h'] at h
        simp only [Set.mem_setOf_eq] at h
        lift a to â„• using h.ne
        use a
        rw [h']
        rfl
      Â· exfalso
        rw [h'] at h
        simp only [Set.mem_setOf_eq, not_top_lt] at h
    | univ => exact âŸ¨0, Set.subset_univ _âŸ©
    | inter t u ht hu ht' hu' =>
      rcases ht' (Set.mem_of_mem_inter_left h) with âŸ¨a, haâŸ©
      rcases hu' (Set.mem_of_mem_inter_right h) with âŸ¨b, hbâŸ©
      refine âŸ¨max a b, ?_âŸ©
      have : @Nat.cast ENat _ (max a b) = max â†‘a â†‘b := by
        apply eq_max
        Â· rw [Nat.cast_le]
          exact le_max_left _ _
        Â· rw [Nat.cast_le]
          exact le_max_right _ _
        Â· intro d h1 h2
          rcases max_choice a b with h | h <;> rwa [h]
      rw [this]
      apply Set.subset_inter
      Â· refine subset_trans ?_ ha
        exact Set.Ioi_subset_Ioi (le_max_left _ _)
      Â· refine subset_trans ?_ hb
        exact Set.Ioi_subset_Ioi (le_max_right _ _)
    | sUnion S hS hS' =>
      simp at h
      rcases h with âŸ¨t, ht1, ht2âŸ©
      rcases hS' t ht1 ht2 with âŸ¨a, haâŸ©
      use a
      apply Set.subset_sUnion_of_subset _ _ ha ht1
  mpr := by
    let _ := Preorder.topology ENat
    have _ : OrderTopology ENat := OrderTopology.mk rfl
    rintro âŸ¨a, haâŸ©
    rw [â† Set.inter_union_compl s (Set.Ioi a)]
    apply IsOpen.union
    Â· rw [Set.inter_eq_self_of_subset_right ha, isOpen_iff_generate_intervals]
      constructor
      exact âŸ¨a, Or.inl rflâŸ©
    Â· apply lol
      simp [h]

instance {X : Type} [TopologicalSpace X] [SequentialSpace X] : CompactlyGeneratedSpace.{0} X := by
  rw [compactlyGeneratedSpace_iff]
  refine fun s â†¦ âŸ¨fun hs _ _ _ _ f hf â†¦ hs.preimage hf,
    fun h â†¦ SequentialSpace.isClosed_of_seq _ fun u p hu hup â†¦ ?_âŸ©
  let _ : TopologicalSpace ENat := Preorder.topology ENat
  have _ : OrderTopology ENat := OrderTopology.mk rfl
  let f : ENat â†’ X := fun n â†¦
    match n with
    | some k => u k
    | none => p
  have hf : Continuous f := by
    constructor
    intro s hs
    by_cases hmm : âŠ¤ âˆˆ (f â»Â¹' s)
    Â· rw [lol' _ hmm]
      have : Filter.Tendsto (fun n : â„• â†¦ f n) Filter.atTop (ð“ p) := by
        simp_rw [f]
        exact hup
      have aux : p âˆˆ s := by
        rw [show p = f âŠ¤ by rfl]
        exact hmm
      rw [tendsto_atTop_nhds] at this
      rcases this s aux hs with âŸ¨N, hNâŸ©
      use N
      intro x hx
      rcases eq_or_ne x âŠ¤ with y | z
      Â· rw [y]
        exact aux
      Â· lift x to â„• using z
        apply hN
        simp at hx
        exact hx.le
    exact lol _ hmm
  rw [show p = f âŠ¤ by rfl]
  change âŠ¤ âˆˆ f â»Â¹' s
  have omg : Filter.Tendsto (fun n â†¦ n : â„• â†’ ENat) Filter.atTop (ð“ âŠ¤) := by
    rw [tendsto_atTop_nhds]
    intro U mem_U hU
    rw [lol' _ mem_U] at hU
    rcases hU with âŸ¨x, hvâŸ©
    use x + 1
    intro n hn
    apply hv
    simp only [Set.mem_Ioi, Nat.cast_lt]
    omega
  apply IsClosed.mem_of_tendsto _ omg
  Â· simp
    use 0
    rintro b -
    exact hu b
  Â· apply h f hf

theorem IsClosed.isClosedMap_subtype_val {X : Type*} [TopologicalSpace X]
    {s : Set X} (hs : IsClosed s) :
    IsClosedMap (@Subtype.val X s) := hs.closedEmbedding_subtype_val.isClosedMap

theorem compactlyGeneratedSpace_iff_of_t2 {X : Type u} [TopologicalSpace X] [T2Space X] :
    CompactlyGeneratedSpace.{u} X â†”
      (âˆ€ s, IsClosed s â†” âˆ€ (K : Set X), IsCompact K â†’ IsClosed (s âˆ© K)) := by
  rw [compactlyGeneratedSpace_iff]
  congrm (âˆ€ s, IsClosed s â†” ?_)
  refine âŸ¨fun h K hK â†¦ ?_, fun h K _ _ _ f hf â†¦ ?_âŸ©
  Â· rw [Set.inter_comm, â† Subtype.image_preimage_coe]
    refine IsClosed.isClosedMap_subtype_val hK.isClosed _ ?_
    exact @h _ _ (isCompact_iff_compactSpace.1 hK) _ _ continuous_subtype_val
  Â· rw [â† Set.preimage_inter_range]
    exact (h _ (isCompact_range hf)).preimage hf

/-- The type of `u`-compactly generated `w`-small topological spaces. -/
structure CompactlyGenerated where
  /-- The underlying topological space of an object of `CompactlyGenerated`. -/
  toTop : TopCat.{w}
  /-- The underlying topological space is compactly generated. -/
  [is_compactly_generated : CompactlyGeneratedSpace.{u} toTop]

namespace CompactlyGenerated

instance : Inhabited CompactlyGenerated.{u, w} :=
  âŸ¨{ toTop := { Î± := ULift (Fin 37) } }âŸ©

instance : CoeSort CompactlyGenerated Type* :=
  âŸ¨fun X => X.toTopâŸ©

attribute [instance] is_compactly_generated

instance : Category.{w, w+1} CompactlyGenerated.{u, w} :=
  InducedCategory.category toTop

instance : ConcreteCategory.{w} CompactlyGenerated.{u, w} :=
  InducedCategory.concreteCategory _

variable (X : Type w) [TopologicalSpace X] [CompactlyGeneratedSpace.{u} X]

/-- Constructor for objects of the category `CompactlyGenerated`. -/
def of : CompactlyGenerated.{u, w} where
  toTop := TopCat.of X
  is_compactly_generated := â€¹_â€º

/-- The fully faithful embedding of `CompactlyGenerated` in `TopCat`. -/
@[simps!]
def compactlyGeneratedToTop : CompactlyGenerated.{u, w} â¥¤ TopCat.{w} :=
  inducedFunctor _

/-- `compactlyGeneratedToTop` is fully faithful. -/
def fullyFaithfulCompactlyGeneratedToTop : compactlyGeneratedToTop.{u, w}.FullyFaithful :=
  fullyFaithfulInducedFunctor _

instance : compactlyGeneratedToTop.{u, w}.Full := fullyFaithfulCompactlyGeneratedToTop.full

instance : compactlyGeneratedToTop.{u, w}.Faithful := fullyFaithfulCompactlyGeneratedToTop.faithful

/-- Construct an isomorphism from a homeomorphism. -/
@[simps hom inv]
def isoOfHomeo {X Y : CompactlyGenerated.{u, w}} (f : X â‰ƒâ‚œ Y) : X â‰… Y where
  hom := âŸ¨f, f.continuousâŸ©
  inv := âŸ¨f.symm, f.symm.continuousâŸ©
  hom_inv_id := by
    ext x
    exact f.symm_apply_apply x
  inv_hom_id := by
    ext x
    exact f.apply_symm_apply x

/-- Construct a homeomorphism from an isomorphism. -/
@[simps]
def homeoOfIso {X Y : CompactlyGenerated.{u, w}} (f : X â‰… Y) : X â‰ƒâ‚œ Y where
  toFun := f.hom
  invFun := f.inv
  left_inv x := by simp
  right_inv x := by simp
  continuous_toFun := f.hom.continuous
  continuous_invFun := f.inv.continuous

/-- The equivalence between isomorphisms in `CompactlyGenerated` and homeomorphisms
of topological spaces. -/
@[simps]
def isoEquivHomeo {X Y : CompactlyGenerated.{u, w}} : (X â‰… Y) â‰ƒ (X â‰ƒâ‚œ Y) where
  toFun := homeoOfIso
  invFun := isoOfHomeo
  left_inv f := by
    ext
    rfl
  right_inv f := by
    ext
    rfl

end CompactlyGenerated
