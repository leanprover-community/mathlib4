/-
Copyright (c) 2024 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz, Dagur Asgeirsson, Filippo A. E. Nuccio, Riccardo Brasca
-/
import Mathlib.CategoryTheory.Functor.ReflectsIso
import Mathlib.Topology.Category.TopCat.Basic
/-!

# Categories of Compact Hausdorff Spaces

We construct the category of compact Hausdorff spaces satisfying an additional property `P`.

## Implementation

We define a structure `CompHausLike` which takes as an argument a predicate `P` on topological
spaces. It consists of the data of a topological space, satisfying the additional properties of
being compact and Hausdorff, and satisfying `P`. We give a category structure to `CompHausLike P`
induced by the forgetful functor to topological spaces.

It used to be the case (before #12930 was merged) that several different categories of compact
Hausdorff spaces, possibly satisfying some extra property, were defined from scratch in this way.
For example, one would define a structure `CompHaus` as follows:

```lean
structure CompHaus where
  toTop : TopCat
  [is_compact : CompactSpace toTop]
  [is_hausdorff : T2Space toTop]
```

and give it the category structure induced from topological spaces. Then the category of profinite
spaces was defined as follows:

```lean
structure Profinite where
  toCompHaus : CompHaus
  [isTotallyDisconnected : TotallyDisconnectedSpace toCompHaus]
```

The categories `Stonean` consisting of extremally disconnected compact Hausdorff spaces and
`LightProfinite` consisting of totally disconnected, second countable compact Hausdorff spaces were
defined in a similar way. This resulted in code duplication, and reducing this duplication was part
of the motivation for introducing `CompHausLike`.

Using `CompHausLike`, we can now define
`CompHaus := CompHausLike (fun _ ‚Ü¶ True)`
`Profinite := CompHausLike (fun X ‚Ü¶ TotallyDisconnectedSpace X)`.
`Stonean := CompHausLike (fun X ‚Ü¶ ExtremallyDisconnected X)`.
`LightProfinite := CompHausLike  (fun X ‚Ü¶ TotallyDisconnectedSpace X ‚àß SecondCountableTopology X)`.

These four categories are important building blocks of condensed objects (see the files
`Condensed.Basic` and `Condensed.Light.Basic`). These categories share many properties and often,
one wants to argue about several of them simultaneously. This is the other part of the motivation
for introducing `CompHausLike`. On paper, one would say "let `C` be on of the categories `CompHaus`
or `Profinite`, then the following holds: ...". This was not possible in Lean using the old
definitions. Using the new definitions, this becomes a matter of identifying what common property
of `CompHaus` and `Profinite` is used in the proof in question, and then proving the theorem for
`CompHausLike P` satisfying that property, and it will automatically apply to both `CompHaus` and
`Profinite`.
-/

universe u

open CategoryTheory

attribute [local instance] ConcreteCategory.instFunLike

variable (P : TopCat.{u} ‚Üí Prop)

/-- The type of Compact Hausdorff topological spaces satisfying an additional property `P`. -/
structure CompHausLike where
  /-- The underlying topological space of an object of `CompHausLike P`. -/
  toTop : TopCat
  /-- The underlying topological space is compact. -/
  [is_compact : CompactSpace toTop]
  /-- The underlying topological space is T2. -/
  [is_hausdorff : T2Space toTop]
  /-- The underlying topological space satisfies P. -/
  prop : P toTop

namespace CompHausLike

attribute [instance] is_compact is_hausdorff

instance : CoeSort (CompHausLike P) (Type u) :=
  ‚ü®fun X => X.toTop‚ü©

instance category : Category (CompHausLike P) :=
  InducedCategory.category toTop

instance concreteCategory : ConcreteCategory (CompHausLike P) :=
  InducedCategory.concreteCategory _

instance hasForget‚ÇÇ : HasForget‚ÇÇ (CompHausLike P) TopCat :=
  InducedCategory.hasForget‚ÇÇ _

variable (X : Type u) [TopologicalSpace X] [CompactSpace X] [T2Space X]

/-- This wraps the predicate `P : TopCat ‚Üí Prop` in a typeclass. -/
class HasProp : Prop where
  hasProp : P (TopCat.of X)

variable [HasProp P X]

/-- A constructor for objects of the category `CompHausLike P`,
taking a type, and bundling the compact Hausdorff topology
found by typeclass inference. -/
def of : CompHausLike P where
  toTop := TopCat.of X
  is_compact := ‚Äπ_‚Ä∫
  is_hausdorff := ‚Äπ_‚Ä∫
  prop := HasProp.hasProp

@[simp]
theorem coe_of : (CompHausLike.of P X : Type _) = X :=
  rfl

@[simp]
theorem coe_id (X : CompHausLike P) : (ùüô ((forget (CompHausLike P)).obj X)) = id :=
  rfl

@[simp]
theorem coe_comp {X Y Z : CompHausLike P} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    ((forget (CompHausLike P)).map f ‚â´ (forget (CompHausLike P)).map g) = g ‚àò f :=
  rfl

-- Note (#10754): Lean does not see through the forgetful functor here
instance (X : CompHausLike.{u} P) : TopologicalSpace ((forget (CompHausLike P)).obj X) :=
  inferInstanceAs (TopologicalSpace X.toTop)

-- Note (#10754): Lean does not see through the forgetful functor here
instance (X : CompHausLike.{u} P) : CompactSpace ((forget (CompHausLike P)).obj X) :=
  inferInstanceAs (CompactSpace X.toTop)

-- Note (#10754): Lean does not see through the forgetful functor here
instance (X : CompHausLike.{u} P) : T2Space ((forget (CompHausLike P)).obj X) :=
  inferInstanceAs (T2Space X.toTop)

variable {P}

/-- If `P` imples `P'`, then there is a functor from `CompHausLike P` to `CompHausLike P'`. -/
@[simps]
def toCompHausLike {P P' : TopCat ‚Üí Prop} (h : ‚àÄ (X : CompHausLike P), P X.toTop ‚Üí P' X.toTop) :
    CompHausLike P ‚•§ CompHausLike P' where
  obj X :=
    have : HasProp P' X := ‚ü®(h _ X.prop)‚ü©
    CompHausLike.of _ X
  map f := f

section

variable {P P' : TopCat ‚Üí Prop} (h : ‚àÄ (X : CompHausLike P), P X.toTop ‚Üí P' X.toTop)

/-- If `P` imples `P'`, then the functor from `CompHausLike P` to `CompHausLike P'` is fully
faithful. -/
def fullyFaithfulToCompHausLike : (toCompHausLike h).FullyFaithful :=
  fullyFaithfulInducedFunctor _

instance : (toCompHausLike h).Full := (fullyFaithfulToCompHausLike h).full

instance : (toCompHausLike h).Faithful := (fullyFaithfulToCompHausLike h).faithful

end

variable (P)

/-- The fully faithful embedding of `CompHausLike P` in `TopCat`. -/
@[simps!]
def compHausLikeToTop : CompHausLike.{u} P ‚•§ TopCat.{u} :=
  inducedFunctor _ -- deriving Full, Faithful -- Porting note: deriving fails, adding manually.

example {P P' : TopCat ‚Üí Prop} (h : ‚àÄ (X : CompHausLike P), P X.toTop ‚Üí P' X.toTop) :
    toCompHausLike h ‚ãô compHausLikeToTop P' = compHausLikeToTop P := rfl

/-- The functor from `CompHausLike P` to `TopCat` is fully faithful. -/
def fullyFaithfulCompHausLikeToTop : (compHausLikeToTop P).FullyFaithful :=
  fullyFaithfulInducedFunctor _

instance : (compHausLikeToTop P).Full  :=
  inferInstanceAs (inducedFunctor _).Full

instance : (compHausLikeToTop P).Faithful :=
  inferInstanceAs (inducedFunctor _).Faithful

instance (X : CompHausLike P) : CompactSpace ((compHausLikeToTop P).obj X) :=
  inferInstanceAs (CompactSpace X.toTop)

instance (X : CompHausLike P) : T2Space ((compHausLikeToTop P).obj X) :=
  inferInstanceAs (T2Space X.toTop)

variable {P}

theorem epi_of_surjective {X Y : CompHausLike.{u} P} (f : X ‚ü∂ Y) (hf : Function.Surjective f) :
    Epi f := by
  rw [‚Üê CategoryTheory.epi_iff_surjective] at hf
  exact (forget (CompHausLike P)).epi_of_epi_map hf

theorem mono_iff_injective {X Y : CompHausLike.{u} P} (f : X ‚ü∂ Y) :
    Mono f ‚Üî Function.Injective f := by
  constructor
  ¬∑ intro hf x‚ÇÅ x‚ÇÇ h
    let g‚ÇÅ : X ‚ü∂ X := ‚ü®fun _ => x‚ÇÅ, continuous_const‚ü©
    let g‚ÇÇ : X ‚ü∂ X := ‚ü®fun _ => x‚ÇÇ, continuous_const‚ü©
    have : g‚ÇÅ ‚â´ f = g‚ÇÇ ‚â´ f := by ext; exact h
    exact ContinuousMap.congr_fun ((cancel_mono _).mp this) x‚ÇÅ
  ¬∑ rw [‚Üê CategoryTheory.mono_iff_injective]
    apply (forget (CompHausLike P)).mono_of_mono_map

/-- Any continuous function on compact Hausdorff spaces is a closed map. -/
theorem isClosedMap {X Y : CompHausLike.{u} P} (f : X ‚ü∂ Y) : IsClosedMap f := fun _ hC =>
  (hC.isCompact.image f.continuous).isClosed

/-- Any continuous bijection of compact Hausdorff spaces is an isomorphism. -/
theorem isIso_of_bijective {X Y : CompHausLike.{u} P} (f : X ‚ü∂ Y) (bij : Function.Bijective f) :
    IsIso f := by
  let E := Equiv.ofBijective _ bij
  have hE : Continuous E.symm := by
    rw [continuous_iff_isClosed]
    intro S hS
    rw [‚Üê E.image_eq_preimage]
    exact isClosedMap f S hS
  refine ‚ü®‚ü®‚ü®E.symm, hE‚ü©, ?_, ?_‚ü©‚ü©
  ¬∑ ext x
    apply E.symm_apply_apply
  ¬∑ ext x
    apply E.apply_symm_apply

instance forget_reflectsIsomorphisms :
    (forget (CompHausLike.{u} P)).ReflectsIsomorphisms :=
  ‚ü®by intro A B f hf; exact isIso_of_bijective _ ((isIso_iff_bijective f).mp hf)‚ü©

/-- Any continuous bijection of compact Hausdorff spaces induces an isomorphism. -/
noncomputable def isoOfBijective {X Y : CompHausLike.{u} P} (f : X ‚ü∂ Y)
    (bij : Function.Bijective f) : X ‚âÖ Y :=
  letI := isIso_of_bijective _ bij
  asIso f

/-- Construct an isomorphism from a homeomorphism. -/
@[simps!]
def isoOfHomeo {X Y : CompHausLike.{u} P} (f : X ‚âÉ‚Çú Y) : X ‚âÖ Y :=
  (fullyFaithfulCompHausLikeToTop P).preimageIso (TopCat.isoOfHomeo f)

/-- Construct a homeomorphism from an isomorphism. -/
@[simps!]
def homeoOfIso {X Y : CompHausLike.{u} P} (f : X ‚âÖ Y) : X ‚âÉ‚Çú Y :=
  TopCat.homeoOfIso <| (compHausLikeToTop P).mapIso f

/-- The equivalence between isomorphisms in `CompHaus` and homeomorphisms
of topological spaces. -/
@[simps]
def isoEquivHomeo {X Y : CompHausLike.{u} P} : (X ‚âÖ Y) ‚âÉ (X ‚âÉ‚Çú Y) where
  toFun := homeoOfIso
  invFun := isoOfHomeo
  left_inv _ := rfl
  right_inv _ := rfl

/-- A constant map as a morphism in `CompHausLike` -/
def const {P : TopCat.{u} ‚Üí Prop}
    (T : CompHausLike.{u} P) {S : CompHausLike.{u} P} (s : S) : T ‚ü∂ S :=
  ContinuousMap.const _ s

lemma const_comp {P : TopCat.{u} ‚Üí Prop} {S T U : CompHausLike.{u} P}
    (s : S) (g : S ‚ü∂ U) : T.const s ‚â´ g = T.const (g s) :=
  rfl

end CompHausLike
