/-
Copyright (c) 2024 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Topology.Algebra.Monoid

/-! # Approximate units

An *approximate unit* is a filter `l` such that multiplication on the left (or right) by `m : Î±`
tends to `ğ“ m` along the filter, and additionally `l â‰  âŠ¥`.

Examples of approximate units include:

- The trivial approximate unit `pure 1` in a normed ring.
- `ğ“ 1` or `ğ“[â‰ ] 1` in a normed ring (note that the latter is disjoint from `pure 1`).
- In a Câ‹†-algebra, the filter generated by the sections `fun a â†¦ {x | a â‰¤ x} âˆ© closedBall 0 1`,
  where `a` ranges over the positive elements of norm strictly less than 1.
-/

open Filter Topology

/-- An *approximate unit* is a proper filter (i.e., `â‰  âŠ¥`) such that multiplication on the left
(and separately on the right) by `m : Î±` tends to `ğ“ m` along the filter. -/
structure Filter.IsApproximateUnit {Î± : Type*} [TopologicalSpace Î±] [Mul Î±]
    (l : Filter Î±) : Prop where
  /-- Multiplication on the left by `m` tends to `ğ“ m` along the filter. -/
  tendsto_mul_left m : Tendsto (m * Â·) l (ğ“ m)
  /-- Multiplication on the right by `m` tends to `ğ“ m` along the filter. -/
  tendsto_mul_right m : Tendsto (Â· * m) l (ğ“ m)
  /-- The filter is not `âŠ¥`. -/
  protected [neBot : NeBot l]

namespace Filter.IsApproximateUnit

section TopologicalMonoid

variable {Î± : Type*} [TopologicalSpace Î±] [MulOneClass Î±]

variable (Î±) in
/-- A unital magma with a topology and bornology has the trivial approximate unit `pure 1`. -/
lemma pure_one : IsApproximateUnit (pure (1 : Î±))  where
  tendsto_mul_left m := by simpa using tendsto_pure_nhds (m * Â·) (1 : Î±)
  tendsto_mul_right m := by simpa using tendsto_pure_nhds (Â· * m) (1 : Î±)

/-- If `l` is an approximate unit and `âŠ¥ < l' â‰¤ l`, then `l'` is also an approximate unit. -/
lemma mono {l l' : Filter Î±} (hl : l.IsApproximateUnit) (hle : l' â‰¤ l) [hl' : l'.NeBot] :
    l'.IsApproximateUnit where
  tendsto_mul_left m := hl.tendsto_mul_left m |>.mono_left hle
  tendsto_mul_right m := hl.tendsto_mul_right m |>.mono_left hle

variable (Î±) in
/-- In a topological unital magma, `ğ“ 1` is an approximate unit. -/
lemma nhds_one [ContinuousMul Î±] : IsApproximateUnit (ğ“ (1 : Î±)) where
  tendsto_mul_left m := by simpa using tendsto_id (x := ğ“ 1) |>.const_mul m
  tendsto_mul_right m := by simpa using tendsto_id (x := ğ“ 1) |>.mul_const m

/-- In a topological unital magma, `ğ“ 1` is the largest approximate unit. -/
lemma iff_neBot_and_le_nhds_one [ContinuousMul Î±] {l : Filter Î±} :
    IsApproximateUnit l â†” l.NeBot âˆ§ l â‰¤ ğ“ 1 :=
  âŸ¨fun hl â†¦ âŸ¨hl.neBot, by simpa using hl.tendsto_mul_left 1âŸ©,
    And.elim fun _ hl â†¦ nhds_one Î± |>.mono hlâŸ©

/-- In a topological unital magma, `ğ“ 1` is the largest approximate unit. -/
lemma iff_le_nhds_one [ContinuousMul Î±] {l : Filter Î±} [l.NeBot] :
    IsApproximateUnit l â†” l â‰¤ ğ“ 1 := by
  simpa [iff_neBot_and_le_nhds_one] using fun _ â†¦ â€¹_â€º


end TopologicalMonoid

end Filter.IsApproximateUnit
