/-
Copyright (c) 2020 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Kim Morrison
-/
module

public import Mathlib.Algebra.Order.Interval.Set.Instances
public import Mathlib.Order.Interval.Set.ProjIcc
public import Mathlib.Topology.Algebra.Ring.Real

/-!
# The unit interval, as a topological space

Use `open unitInterval` to turn on the notation `I := Set.Icc (0 : â„) (1 : â„)`.

We provide basic instances, as well as a custom tactic for discharging
`0 â‰¤ â†‘x`, `0 â‰¤ 1 - â†‘x`, `â†‘x â‰¤ 1`, and `1 - â†‘x â‰¤ 1` when `x : I`.

-/

@[expose] public section

noncomputable section

open Topology Filter Set Int Set.Icc

/-! ### The unit interval -/


/-- The unit interval `[0,1]` in â„. -/
abbrev unitInterval : Set â„ :=
  Set.Icc 0 1

@[inherit_doc]
scoped[unitInterval] notation "I" => unitInterval

namespace unitInterval

theorem zero_mem : (0 : â„) âˆˆ I :=
  âŸ¨le_rfl, zero_le_oneâŸ©

theorem one_mem : (1 : â„) âˆˆ I :=
  âŸ¨zero_le_one, le_rflâŸ©

theorem mul_mem {x y : â„} (hx : x âˆˆ I) (hy : y âˆˆ I) : x * y âˆˆ I :=
  âŸ¨mul_nonneg hx.1 hy.1, mul_le_oneâ‚€ hx.2 hy.1 hy.2âŸ©

theorem div_mem {x y : â„} (hx : 0 â‰¤ x) (hy : 0 â‰¤ y) (hxy : x â‰¤ y) : x / y âˆˆ I :=
  âŸ¨div_nonneg hx hy, div_le_one_of_leâ‚€ hxy hyâŸ©

/-- The midpoint of the unit interval. -/
def half : I := âŸ¨1 / 2, by constructor <;> linarithâŸ©

theorem fract_mem (x : â„) : fract x âˆˆ I :=
  âŸ¨fract_nonneg _, (fract_lt_one _).leâŸ©

@[deprecated (since := "2025-08-14")] alias mem_iff_one_sub_mem := Icc.mem_iff_one_sub_mem

lemma univ_eq_Icc : (univ : Set I) = Icc (0 : I) (1 : I) := Icc_bot_top.symm

@[norm_cast] theorem coe_ne_zero {x : I} : (x : â„) â‰  0 â†” x â‰  0 := coe_eq_zero.not
@[norm_cast] theorem coe_ne_one {x : I} : (x : â„) â‰  1 â†” x â‰  1 := coe_eq_one.not
@[simp, norm_cast] theorem coe_pos {x : I} : (0 : â„) < x â†” 0 < x := Iff.rfl
@[simp, norm_cast] theorem coe_lt_one {x : I} : (x : â„) < 1 â†” x < 1 := Iff.rfl

theorem mul_le_left {x y : I} : x * y â‰¤ x :=
  Subtype.coe_le_coe.mp <| mul_le_of_le_one_right x.2.1 y.2.2

theorem mul_le_right {x y : I} : x * y â‰¤ y :=
  Subtype.coe_le_coe.mp <| mul_le_of_le_one_left y.2.1 x.2.2

theorem eq_closedBall : I = Metric.closedBall 2â»Â¹ 2â»Â¹ := by
  norm_num [unitInterval, Real.Icc_eq_closedBall]

/-- Unit interval central symmetry. -/
def symm : I â†’ I := fun t => âŸ¨1 - t, Icc.mem_iff_one_sub_mem.mp t.propâŸ©

@[inherit_doc]
scoped notation "Ïƒ" => unitInterval.symm

@[simp, grind =]
theorem symm_zero : Ïƒ 0 = 1 :=
  Subtype.ext <| by simp [symm]

@[simp, grind =]
theorem symm_one : Ïƒ 1 = 0 :=
  Subtype.ext <| by simp [symm]

@[simp, grind =]
theorem symm_symm (x : I) : Ïƒ (Ïƒ x) = x :=
  Subtype.ext <| by simp [symm]

theorem symm_involutive : Function.Involutive (symm : I â†’ I) := symm_symm

theorem symm_bijective : Function.Bijective (symm : I â†’ I) := symm_involutive.bijective

@[simp, grind =]
theorem coe_symm_eq (x : I) : (Ïƒ x : â„) = 1 - x :=
  rfl

lemma image_coe_preimage_symm {s : Set I} :
    Subtype.val '' (Ïƒ â»Â¹' s) = (1 - Â·) â»Â¹' (Subtype.val '' s) := by
  simp [symm_involutive, â† Function.Involutive.image_eq_preimage_symm, image_image]

@[simp]
theorem symm_projIcc (x : â„) :
    symm (projIcc 0 1 zero_le_one x) = projIcc 0 1 zero_le_one (1 - x) := by
  ext
  rcases le_total x 0 with hâ‚€ | hâ‚€
  Â· simp [projIcc_of_le_left, projIcc_of_right_le, hâ‚€]
  Â· rcases le_total x 1 with hâ‚ | hâ‚
    Â· lift x to I using âŸ¨hâ‚€, hâ‚âŸ©
      simp_rw [â† coe_symm_eq, projIcc_val]
    Â· simp [projIcc_of_le_left, projIcc_of_right_le, hâ‚]

@[continuity, fun_prop]
theorem continuous_symm : Continuous Ïƒ :=
  Continuous.subtype_mk (by fun_prop) _

/-- `unitInterval.symm` as a `Homeomorph`. -/
@[simps]
def symmHomeomorph : I â‰ƒâ‚œ I where
  toFun := symm
  invFun := symm
  left_inv := symm_symm
  right_inv := symm_symm

theorem strictAnti_symm : StrictAnti Ïƒ := fun _ _ h â†¦ sub_lt_sub_left (Î± := â„) h _


@[simp]
theorem symm_inj {i j : I} : Ïƒ i = Ïƒ j â†” i = j := symm_bijective.injective.eq_iff

theorem half_le_symm_iff (t : I) : 1 / 2 â‰¤ (Ïƒ t : â„) â†” (t : â„) â‰¤ 1 / 2 := by
  rw [coe_symm_eq, le_sub_iff_add_le, add_comm, â† le_sub_iff_add_le, sub_half]

@[simp]
lemma symm_eq_one {i : I} : Ïƒ i = 1 â†” i = 0 := by
  rw [â† symm_zero, symm_inj]

@[simp]
lemma symm_eq_zero {i : I} : Ïƒ i = 0 â†” i = 1 := by
  rw [â† symm_one, symm_inj]

@[simp]
theorem symm_le_symm {i j : I} : Ïƒ i â‰¤ Ïƒ j â†” j â‰¤ i := by
  simp only [symm, Subtype.mk_le_mk, sub_le_sub_iff, add_le_add_iff_left, Subtype.coe_le_coe]

theorem le_symm_comm {i j : I} : i â‰¤ Ïƒ j â†” j â‰¤ Ïƒ i := by
  rw [â† symm_le_symm, symm_symm]

theorem symm_le_comm {i j : I} : Ïƒ i â‰¤ j â†” Ïƒ j â‰¤ i := by
  rw [â† symm_le_symm, symm_symm]

@[simp]
theorem symm_lt_symm {i j : I} : Ïƒ i < Ïƒ j â†” j < i := by
  simp only [symm, Subtype.mk_lt_mk, sub_lt_sub_iff_left, Subtype.coe_lt_coe]

theorem lt_symm_comm {i j : I} : i < Ïƒ j â†” j < Ïƒ i := by
  rw [â† symm_lt_symm, symm_symm]

theorem symm_lt_comm {i j : I} : Ïƒ i < j â†” Ïƒ j < i := by
  rw [â† symm_lt_symm, symm_symm]

instance : ConnectedSpace I :=
  Subtype.connectedSpace âŸ¨nonempty_Icc.mpr zero_le_one, isPreconnected_IccâŸ©

/-- Verify there is an instance for `CompactSpace I`. -/
example : CompactSpace I := by infer_instance

theorem nonneg (x : I) : 0 â‰¤ (x : â„) :=
  x.2.1

theorem one_minus_nonneg (x : I) : 0 â‰¤ 1 - (x : â„) := by simpa using x.2.2

theorem le_one (x : I) : (x : â„) â‰¤ 1 :=
  x.2.2

theorem one_minus_le_one (x : I) : 1 - (x : â„) â‰¤ 1 := by simpa using x.2.1

theorem add_pos {t : I} {x : â„} (hx : 0 < x) : 0 < (x + t : â„) :=
  add_pos_of_pos_of_nonneg hx <| nonneg _

/-- like `unitInterval.nonneg`, but with the inequality in `I`. -/
theorem nonneg' {t : I} : 0 â‰¤ t :=
  t.2.1

/-- like `unitInterval.le_one`, but with the inequality in `I`. -/
theorem le_one' {t : I} : t â‰¤ 1 :=
  t.2.2

protected lemma pos_iff_ne_zero {x : I} : 0 < x â†” x â‰  0 := bot_lt_iff_ne_bot

protected lemma lt_one_iff_ne_one {x : I} : x < 1 â†” x â‰  1 := lt_top_iff_ne_top

lemma eq_one_or_eq_zero_of_le_mul {i j : I} (h : i â‰¤ j * i) : i = 0 âˆ¨ j = 1 := by
  contrapose! h
  rw [â† unitInterval.lt_one_iff_ne_one, â† coe_lt_one, â† unitInterval.pos_iff_ne_zero,
    â† coe_pos] at h
  rw [â† Subtype.coe_lt_coe, coe_mul]
  simpa using mul_lt_mul_of_pos_right h.right h.left

instance : Nontrivial I := âŸ¨âŸ¨1, 0, (one_ne_zero <| congrArg Subtype.val Â·)âŸ©âŸ©

theorem mul_pos_mem_iff {a t : â„} (ha : 0 < a) : a * t âˆˆ I â†” t âˆˆ Set.Icc (0 : â„) (1 / a) := by
  constructor <;> rintro âŸ¨hâ‚, hâ‚‚âŸ© <;> constructor
  Â· exact nonneg_of_mul_nonneg_right hâ‚ ha
  Â· rwa [le_div_iffâ‚€ ha, mul_comm]
  Â· exact mul_nonneg ha.le hâ‚
  Â· rwa [le_div_iffâ‚€ ha, mul_comm] at hâ‚‚

theorem two_mul_sub_one_mem_iff {t : â„} : 2 * t - 1 âˆˆ I â†” t âˆˆ Set.Icc (1 / 2 : â„) 1 := by
  constructor <;> rintro âŸ¨hâ‚, hâ‚‚âŸ© <;> constructor <;> linarith

/-- The unit interval as a submonoid of â„. -/
def submonoid : Submonoid â„ where
  carrier := unitInterval
  one_mem' := unitInterval.one_mem
  mul_mem' := unitInterval.mul_mem

@[simp] theorem coe_unitIntervalSubmonoid : submonoid = unitInterval := rfl
@[simp] theorem mem_unitIntervalSubmonoid {x} : x âˆˆ submonoid â†” x âˆˆ unitInterval :=
  Iff.rfl

protected theorem prod_mem {Î¹ : Type*} {t : Finset Î¹} {f : Î¹ â†’ â„}
    (h : âˆ€ c âˆˆ t, f c âˆˆ unitInterval) :
    âˆ c âˆˆ t, f c âˆˆ unitInterval := _root_.prod_mem (S := unitInterval.submonoid) h

instance : LinearOrderedCommMonoidWithZero I where
  zero_mul i := zero_mul i
  mul_zero i := mul_zero i
  zero_le_one := nonneg'
  mul_le_mul_left i j h_ij k := by simp only [â† Subtype.coe_le_coe, coe_mul]; gcongr; exact nonneg k

lemma subtype_Iic_eq_Icc (x : I) : Subtype.val â»Â¹' (Iic â†‘x) = Icc 0 x := by
  rw [preimage_subtype_val_Iic]
  exact Icc_bot.symm

lemma subtype_Iio_eq_Ico (x : I) : Subtype.val â»Â¹' (Iio â†‘x) = Ico 0 x := by
  rw [preimage_subtype_val_Iio]
  exact Ico_bot.symm

lemma subtype_Ici_eq_Icc (x : I) : Subtype.val â»Â¹' (Ici â†‘x) = Icc x 1 := by
  rw [preimage_subtype_val_Ici]
  exact Icc_top.symm

lemma subtype_Ioi_eq_Ioc (x : I) : Subtype.val â»Â¹' (Ioi â†‘x) = Ioc x 1 := by
  rw [preimage_subtype_val_Ioi]
  exact Ioc_top.symm

end unitInterval

section partition

namespace Set.Icc

variable {Î±} [AddCommGroup Î±] [LinearOrder Î±] [IsOrderedAddMonoid Î±]
  {a b c d : Î±} (h : a â‰¤ b) {Î´ : Î±}

-- TODO: Set.projIci, Set.projIic
/-- `Set.projIcc` is a contraction. -/
lemma _root_.Set.abs_projIcc_sub_projIcc : (|projIcc a b h c - projIcc a b h d| : Î±) â‰¤ |c - d| := by
  wlog hdc : d â‰¤ c generalizing c d
  Â· rw [abs_sub_comm, abs_sub_comm c]; exact this (le_of_not_ge hdc)
  rw [abs_eq_self.2 (sub_nonneg.2 hdc),
    abs_eq_self.2 (sub_nonneg.2 <| mod_cast monotone_projIcc h hdc)]
  rw [â† sub_nonneg] at hdc
  refine (max_sub_max_le_max _ _ _ _).trans (max_le (by rwa [sub_self]) ?_)
  refine ((le_abs_self _).trans <| abs_min_sub_min_le_max _ _ _ _).trans (max_le ?_ ?_)
  Â· rwa [sub_self, abs_zero]
  Â· exact (abs_eq_self.mpr hdc).le

/-- When `h : a â‰¤ b` and `Î´ > 0`, `addNSMul h Î´` is a sequence of points in the closed interval
`[a,b]`, which is initially equally spaced but eventually stays at the right endpoint `b`. -/
def addNSMul (Î´ : Î±) (n : â„•) : Icc a b := projIcc a b h (a + n â€¢ Î´)

omit [IsOrderedAddMonoid Î±] in
lemma addNSMul_zero : addNSMul h Î´ 0 = a := by
  rw [addNSMul, zero_smul, add_zero, projIcc_left]

lemma addNSMul_eq_right [Archimedean Î±] (hÎ´ : 0 < Î´) :
    âˆƒ m, âˆ€ n â‰¥ m, addNSMul h Î´ n = b := by
  obtain âŸ¨m, hmâŸ© := Archimedean.arch (b - a) hÎ´
  refine âŸ¨m, fun n hn â†¦ ?_âŸ©
  rw [addNSMul, coe_projIcc, add_comm, min_eq_left_iff.mpr, max_eq_right h]
  exact sub_le_iff_le_add.mp (hm.trans <| nsmul_le_nsmul_left hÎ´.le hn)

lemma monotone_addNSMul (hÎ´ : 0 â‰¤ Î´) : Monotone (addNSMul h Î´) :=
  fun _ _ hnm â†¦ monotone_projIcc h <| (add_le_add_iff_left _).mpr (nsmul_le_nsmul_left hÎ´ hnm)

lemma abs_sub_addNSMul_le (hÎ´ : 0 â‰¤ Î´) {t : Icc a b} (n : â„•)
    (ht : t âˆˆ Icc (addNSMul h Î´ n) (addNSMul h Î´ (n + 1))) :
    (|t - addNSMul h Î´ n| : Î±) â‰¤ Î´ :=
  calc
    (|t - addNSMul h Î´ n| : Î±) = t - addNSMul h Î´ n := abs_eq_self.2 <| sub_nonneg.2 ht.1
    _ â‰¤ projIcc a b h (a + (n + 1) â€¢ Î´) - addNSMul h Î´ n := by apply sub_le_sub_right; exact ht.2
    _ â‰¤ (|projIcc a b h (a + (n + 1) â€¢ Î´) - addNSMul h Î´ n| : Î±) := le_abs_self _
    _ â‰¤ |a + (n + 1) â€¢ Î´ - (a + n â€¢ Î´)| := abs_projIcc_sub_projIcc h
    _ â‰¤ Î´ := by
          rw [add_sub_add_comm, sub_self, zero_add, succ_nsmul', add_sub_cancel_right]
          exact (abs_eq_self.mpr hÎ´).le

/--
Form a convex linear combination of two points in a closed interval.

This should be removed once a general theory of convex spaces is available in Mathlib.
-/
def convexCombo {a b : â„} (x y : Icc a b) (t : unitInterval) : Icc a b :=
  âŸ¨(1 - t) * x + t * y, by
    constructor
    Â· nlinarith [x.2.1, y.2.1, t.2.1, t.2.2]
    Â· nlinarith [x.2.2, y.2.2, t.2.1, t.2.2]âŸ©

@[simp, grind =]
theorem coe_convexCombo {a b : â„} (x y : Icc a b) (t : unitInterval) :
  (convexCombo x y t : â„) = (1 - t) * x + t * y := rfl

@[simp, grind =]
theorem convexCombo_zero {a b : â„} (x y : Icc a b) : convexCombo x y 0 = x := by
  simp [convexCombo]

@[simp, grind =]
theorem convexCombo_one {a b : â„} (x y : Icc a b) : convexCombo x y 1 = y := by
  simp [convexCombo]

@[simp, grind =]
theorem convexCombo_symm {a b : â„} (x y : Icc a b) (t : unitInterval) :
    convexCombo x y (unitInterval.symm t) = convexCombo y x t := by
  simp [convexCombo]
  abel

@[grind .]
theorem le_convexCombo {a b : â„} {x y : Icc a b} (h : x â‰¤ y) (t : unitInterval) :
    x â‰¤ convexCombo x y t := by
  rw [â† Subtype.coe_le_coe] at h âŠ¢
  simp
  nlinarith [t.2.1, t.2.2]

@[grind .]
theorem convexCombo_le {a b : â„} {x y : Icc a b} (h : x â‰¤ y) (t : unitInterval) :
    convexCombo x y t â‰¤ y := by
  rw [â† Subtype.coe_le_coe] at h âŠ¢
  simp
  nlinarith [t.2.1, t.2.2]

/--
Helper definition for `convexCombo_assoc`, giving one of the coefficients appearing
when we reassociate a convex combination.
-/
abbrev convexCombo_assoc_coeffâ‚ (s t : unitInterval) : unitInterval :=
  âŸ¨s * (1 - t) / (1 - s * t),
    by
      apply div_nonneg
      Â· nlinarith [s.2.1, t.2.2]
      Â· nlinarith [s.2.2, t.2.2, t.2.1],
    by
      apply div_le_one_of_leâ‚€
      Â· nlinarith [s.2.2]
      Â· nlinarith [s.2.2, t.2.2, t.2.1]âŸ©

/--
Helper definition for `convexCombo_assoc`, giving one of the coefficients appearing
when we reassociate a convex combination.
-/
abbrev convexCombo_assoc_coeffâ‚‚ (s t : unitInterval) : unitInterval := s * t

theorem convexCombo_assoc {a b : â„} (x y z : Icc a b) (s t : unitInterval) :
    convexCombo x (convexCombo y z t) s =
      convexCombo (convexCombo x y (convexCombo_assoc_coeffâ‚ s t)) z
        (convexCombo_assoc_coeffâ‚‚ s t) := by
  simp only [convexCombo, coe_mul, Subtype.mk.injEq]
  by_cases hs : (s : â„) = 1
  Â· simp only [hs]
    by_cases ht : (t : â„) = 1
    Â· simp [ht]
    Â· have : (1 - t : â„) â‰  0 := by grind
      field_simp
      simp
  Â· by_cases ht : (t : â„) = 1
    Â· simp [ht]
    Â· have : (1 - s * t : â„) â‰  0 := by
        intro h
        have : 1 â‰¤ (t : â„) := by nlinarith [s.2.2, t.2.1]
        grind
      field_simp
      ring_nf

/--
Helper definition for `convexCombo_assoc'`, giving one of the coefficients appearing
when we reassociate a convex combination in the reverse direction.
-/
abbrev convexCombo_assoc_coeffâ‚' (s t : unitInterval) : unitInterval :=
  unitInterval.symm (convexCombo_assoc_coeffâ‚‚ (unitInterval.symm t) (unitInterval.symm s))

/--
Helper definition for `convexCombo_assoc'`, giving one of the coefficients appearing
when we reassociate a convex combination in the reverse direction.
-/
abbrev convexCombo_assoc_coeffâ‚‚' (s t : unitInterval) : unitInterval :=
  unitInterval.symm (convexCombo_assoc_coeffâ‚ (unitInterval.symm t) (unitInterval.symm s))

theorem convexCombo_assoc' {a b : â„} (x y z : Icc a b) (s t : unitInterval) :
    convexCombo (convexCombo x y s) z t =
      convexCombo x (convexCombo y z (convexCombo_assoc_coeffâ‚‚' s t))
        (convexCombo_assoc_coeffâ‚' s t) := by
  rw [â† convexCombo_symm, â† convexCombo_symm y x, convexCombo_assoc, â† convexCombo_symm x,
    â† convexCombo_symm z y]
  rw [convexCombo_assoc_coeffâ‚', convexCombo_assoc_coeffâ‚‚', unitInterval.symm_symm]

set_option backward.privateInPublic true in
private theorem eq_convexCombo.zero_le {a b : â„} {x y z : Icc a b} (hxy : x â‰¤ y) (hyz : y â‰¤ z) :
    0 â‰¤ ((y - x) / (z - x) : â„) := by
  by_cases h : (z - x : â„) = 0
  Â· simp_all
  Â· replace hxy : (x : â„) â‰¤ (y : â„) := hxy
    replace hyz : (y : â„) â‰¤ (z : â„) := hyz
    apply div_nonneg <;> grind

set_option backward.privateInPublic true in
private theorem eq_convexCombo.le_one {a b : â„} {x y z : Icc a b} (hxy : x â‰¤ y) (hyz : y â‰¤ z) :
    ((y - x) / (z - x) : â„) â‰¤ 1 := by
  by_cases h : (z - x : â„) = 0
  Â· simp_all
  Â· replace hxy : (x : â„) â‰¤ (y : â„) := hxy
    replace hyz : (y : â„) â‰¤ (z : â„) := hyz
    apply div_le_one_of_leâ‚€ <;> grind

set_option backward.privateInPublic true in
set_option backward.privateInPublic.warn false in
/--
A point between two points in a closed interval
can be expressed as a convex combination of them.
-/
theorem eq_convexCombo {a b : â„} {x y z : Icc a b} (hxy : x â‰¤ y) (hyz : y â‰¤ z) :
    y = convexCombo x z âŸ¨((y - x) / (z - x)),
          eq_convexCombo.zero_le hxy hyz, eq_convexCombo.le_one hxy hyzâŸ© := by
  ext
  simp only [coe_convexCombo]
  by_cases h : (z - x : â„) = 0
  Â· simp_all only [div_zero, sub_zero, one_mul, zero_mul, add_zero]
    replace hxy : (x : â„) â‰¤ (y : â„) := hxy
    replace hyz : (y : â„) â‰¤ (z : â„) := hyz
    linarith
  Â· field_simp
    ring_nf

theorem continuous_convexCombo {a b : â„} :
    Continuous (fun (p : Icc a b Ã— Icc a b Ã— unitInterval) => convexCombo p.1 p.2.1 p.2.2) := by
  apply Continuous.subtype_mk
  fun_prop


end Set.Icc

open scoped unitInterval

/-- Any open cover `c` of a closed interval `[a, b]` in â„
can be refined to a finite partition into subintervals. -/
lemma exists_monotone_Icc_subset_open_cover_Icc {Î¹} {a b : â„} (h : a â‰¤ b) {c : Î¹ â†’ Set (Icc a b)}
    (hcâ‚ : âˆ€ i, IsOpen (c i)) (hcâ‚‚ : univ âŠ† â‹ƒ i, c i) : âˆƒ t : â„• â†’ Icc a b, t 0 = a âˆ§
      Monotone t âˆ§ (âˆƒ m, âˆ€ n â‰¥ m, t n = b) âˆ§ âˆ€ n, âˆƒ i, Icc (t n) (t (n + 1)) âŠ† c i := by
  obtain âŸ¨Î´, Î´_pos, ball_subsetâŸ© := lebesgue_number_lemma_of_metric isCompact_univ hcâ‚ hcâ‚‚
  have hÎ´ := half_pos Î´_pos
  refine âŸ¨addNSMul h (Î´/2), addNSMul_zero h,
    monotone_addNSMul h hÎ´.le, addNSMul_eq_right h hÎ´, fun n â†¦ ?_âŸ©
  obtain âŸ¨i, hsubâŸ© := ball_subset (addNSMul h (Î´ / 2) n) trivial
  exact âŸ¨i, fun t ht â†¦ hsub ((abs_sub_addNSMul_le h hÎ´.le n ht).trans_lt <| half_lt_self Î´_pos)âŸ©

/-- Any open cover of the unit interval can be refined to a finite partition into subintervals. -/
lemma exists_monotone_Icc_subset_open_cover_unitInterval {Î¹} {c : Î¹ â†’ Set I}
    (hcâ‚ : âˆ€ i, IsOpen (c i)) (hcâ‚‚ : univ âŠ† â‹ƒ i, c i) : âˆƒ t : â„• â†’ I, t 0 = 0 âˆ§
      Monotone t âˆ§ (âˆƒ n, âˆ€ m â‰¥ n, t m = 1) âˆ§ âˆ€ n, âˆƒ i, Icc (t n) (t (n + 1)) âŠ† c i := by
  simp_rw [â† Subtype.coe_inj]
  exact exists_monotone_Icc_subset_open_cover_Icc zero_le_one hcâ‚ hcâ‚‚

/-- Finite partition variant: Any open cover of `[a, b]` can be refined to a finite partition
with strictly monotone partition points indexed by `Fin (n + 1)`. -/
lemma exists_strictMono_Icc_subset_open_cover_Icc {Î¹} {a b : â„} (h : a â‰¤ b) {c : Î¹ â†’ Set (Icc a b)}
    (hcâ‚ : âˆ€ i, IsOpen (c i)) (hcâ‚‚ : univ âŠ† â‹ƒ i, c i) :
    âˆƒ (n : â„•) (t : Fin (n + 1) â†’ Icc a b),
      StrictMono t âˆ§ t 0 = a âˆ§ t (Fin.last n) = b âˆ§
      âˆ€ i : Fin n, âˆƒ j : Î¹, Icc (t i.castSucc) (t i.succ) âŠ† c j := by
  -- Get Lebesgue number
  obtain âŸ¨Î´, Î´_pos, hÎ´âŸ© := lebesgue_number_lemma_of_metric isCompact_univ hcâ‚ hcâ‚‚
  -- Pick n: if a = b then n = 0, otherwise pick n large enough so that (b - a) / n < Î´
  by_cases hab : a = b
  Â· -- Case a = b: take n = 0 with single partition point
    subst hab
    refine âŸ¨0, fun _ => âŸ¨a, by simpâŸ©, ?_, ?_, ?_, ?_âŸ©
    Â· -- StrictMono: vacuously true for Fin 1
      intro i j hij
      exact absurd hij (by omega)
    Â· -- t 0 = a
      rfl
    Â· -- t (Fin.last 0) = a = b
      rfl
    Â· -- Covering property: vacuously true for Fin 0
      intro i
      exact absurd i.val.lt_succ_self (by omega)
  Â· -- Case a < b: pick n with (b - a) / n < Î´
    have hab_pos : 0 < b - a := sub_pos.mpr (Ne.lt_of_le hab h)
    obtain âŸ¨n, hn_pos, hn_smallâŸ© : âˆƒ n : â„•, 0 < n âˆ§ (b - a) / n < Î´ := by
      obtain âŸ¨n, hnâŸ© := exists_nat_gt ((b - a) / Î´)
      have hn_pos : 0 < n := by
        have h1 : 0 < (b - a) / Î´ := div_pos hab_pos Î´_pos
        have h2 : (0 : â„) < n := by linarith
        exact Nat.cast_pos.mp h2
      refine âŸ¨n, hn_pos, ?_âŸ©
      have hn_pos' : (0 : â„) < n := Nat.cast_pos.mpr hn_pos
      -- From (b - a) / Î´ < n, multiply both sides by Î´ to get b - a < n * Î´
      have h_mul : b - a < n * Î´ := calc
        b - a = (b - a) / Î´ * Î´ := by field_simp [Î´_pos.ne']
        _ < n * Î´ := by nlinarith [Î´_pos]
      calc (b - a) / n < (n * Î´) / n := by gcongr
        _ = Î´ := by field_simp
    -- Define partition: t k = a + k * (b - a) / n
    let t : Fin (n + 1) â†’ Icc a b := fun k => âŸ¨a + k * (b - a) / n, by
    constructor
    Â· linarith [mul_nonneg (Nat.cast_nonneg (k : â„•)) (sub_nonneg.mpr h),
        div_nonneg (mul_nonneg (Nat.cast_nonneg (k : â„•)) (sub_nonneg.mpr h)) (Nat.cast_nonneg n)]
    Â· have hk : (k : â„) â‰¤ n := Nat.cast_le.mpr (Nat.lt_succ_iff.mp k.is_lt)
      have hn_pos' : (0 : â„) < n := Nat.cast_pos.mpr hn_pos
      calc a + k * (b - a) / n â‰¤ a + n * (b - a) / n := by {
            have : k * (b - a) â‰¤ n * (b - a) := by nlinarith
            linarith [div_le_div_of_nonneg_right this hn_pos'.le] }
        _ = b := by field_simp [hn_pos'.ne']; ringâŸ©
    refine âŸ¨n, t, ?_, ?_, ?_, ?_âŸ©
    Â· -- StrictMono
      intro i j hij
      change (t i : â„) < (t j : â„)
      simp only [t]
      have hij' : (i : â„) < (j : â„) := Nat.cast_lt.mpr hij
      have hn_pos' : (0 : â„) < n := Nat.cast_pos.mpr hn_pos
      have : i * (b - a) < j * (b - a) := by nlinarith [hab_pos]
      linarith [div_lt_div_of_pos_right this hn_pos']
    Â· -- t 0 = a
      simp [t]
    Â· -- t (Fin.last n) = b
      simp [t]
      field_simp [Nat.cast_pos.mpr hn_pos]
      ring
    Â· -- Covering property
      intro i
      -- Use StrictMono to get that t i.castSucc < t i.succ
      have h_mono : (t i.castSucc : â„) < (t i.succ : â„) := by
        simp only [t]
        have hij : (i.castSucc : â„•) < (i.succ : â„•) := by
          rw [Fin.val_castSucc]
          simp
        have hij' : (i.castSucc : â„) < (i.succ : â„) := Nat.cast_lt.mpr hij
        have hn_pos' : (0 : â„) < n := Nat.cast_pos.mpr hn_pos
        have : i.castSucc * (b - a) < i.succ * (b - a) := by nlinarith [hab_pos]
        linarith [div_lt_div_of_pos_right this hn_pos']
      -- Define the midpoint
      let m : Icc a b := âŸ¨((t i.castSucc : â„) + (t i.succ : â„)) / 2, by
        constructor
        Â· linarith [(t i.castSucc).2.1, (t i.succ).2.1]
        Â· linarith [(t i.castSucc).2.2, (t i.succ).2.2]âŸ©
      -- The segment is contained in ball m Î´
      have h_subset : Icc (t i.castSucc) (t i.succ) âŠ† Metric.ball m Î´ := by
        intro x hx
        simp only [Metric.ball, mem_setOf_eq]
        -- The segment has length (b-a)/n, so max distance from midpoint is (b-a)/(2n)
        have segment_len : (t i.succ : â„) - (t i.castSucc : â„) = (b - a) / n := by
          simp [t]
          field_simp
          ring
        -- x is in the segment, so its distance from midpoint is at most (b-a)/(2n) < Î´
        have hx_bounds : (t i.castSucc : â„) â‰¤ (x : â„) âˆ§ (x : â„) â‰¤ (t i.succ : â„) := âŸ¨hx.1, hx.2âŸ©
        have dist_bound : dist (x : â„) (m : â„) â‰¤ ((b - a) / n) / 2 := by
          rw [dist_comm, Real.dist_eq]
          simp only [m, abs_sub_le_iff]
          constructor
          Â· linarith [hx_bounds.1, hx_bounds.2]
          Â· linarith [hx_bounds.1, hx_bounds.2]
        -- Since (b-a)/n < Î´, we have (b-a)/(2n) < Î´/2 < Î´
        calc dist (x : â„) (m : â„) â‰¤ ((b - a) / n) / 2 := dist_bound
          _ < Î´ / 2 := by linarith [hn_small]
          _ < Î´ := by linarith [Î´_pos]
      -- Apply Lebesgue number property to get the covering set
      obtain âŸ¨j, hjâŸ© := hÎ´ m trivial
      exact âŸ¨j, Subset.trans h_subset hjâŸ©

  -- Alternative proof:
  -- obtain âŸ¨Î´, Î´_pos, ball_subsetâŸ© := lebesgue_number_lemma_of_metric isCompact_univ hcâ‚ hcâ‚‚
  -- refine (lt_or_eq_of_le h).elim ?_ ?_
  -- Â· intro hlt
  --   have hab_pos : 0 < b - a := sub_pos.mpr hlt
  --   obtain âŸ¨n, hn_gtâŸ© := exists_nat_gt ((b - a) / Î´)
  --   have h0_le : (0 : â„) â‰¤ (b - a) / Î´ := div_nonneg (sub_nonneg.mpr h) Î´_pos.le
  --   have hn_pos' : (0 : â„) < (n : â„) := lt_of_le_of_lt h0_le hn_gt
  --   have hn_pos : 0 < n := by exact_mod_cast hn_pos'
  --   have hÎ´_ne : Î´ â‰  0 := ne_of_gt Î´_pos
  --   have hmul_lt : b - a < (n : â„) * Î´ := by
  --     have := mul_lt_mul_of_pos_right hn_gt Î´_pos
  --     simpa [div_eq_mul_inv, hÎ´_ne, mul_comm, mul_left_comm, mul_assoc] using this
  --   let Î” : â„ := (b - a) / (n : â„)
  --   have hÎ”_pos : 0 < Î” := div_pos hab_pos hn_pos'
  --   have hÎ”_nonneg : 0 â‰¤ Î” := hÎ”_pos.le
  --   have hn_ne_zero : (n : â„) â‰  0 :=
  --     by exact_mod_cast (Nat.cast_ne_zero.mpr <| ne_of_gt hn_pos)
  --   have hÎ”_lt : Î” < Î´ := by
  --     have := mul_lt_mul_of_pos_left hmul_lt (inv_pos.mpr hn_pos')
  --     simpa [Î”, div_eq_mul_inv, hn_ne_zero, mul_comm, mul_left_comm, mul_assoc] using this
  --   let t : Fin (n + 1) â†’ Icc a b := fun k =>
  --     âŸ¨a + (k : â„) * Î”, by
  --       have hk : (k : â„) â‰¤ n := by exact_mod_cast Fin.le_last k
  --       refine âŸ¨?_, ?_âŸ©
  --       Â· exact le_add_of_nonneg_right (mul_nonneg (Nat.cast_nonneg _) hÎ”_nonneg)
  --       Â· have hk' : (k : â„) * Î” â‰¤ (n : â„) * Î” :=
  --           mul_le_mul_of_nonneg_right hk hÎ”_nonneg
  --         have hN : (n : â„) * Î” = b - a := by
  --           have hn_ne : (n : â„) â‰  0 := hn_ne_zero
  --           have : ((b - a) / (n : â„)) * (n : â„) = b - a := by
  --             field_simp [hn_ne]
  --           simpa [Î”, mul_comm] using this
  --         have hk'' := hk'
  --         simp [hN] at hk''
  --         simpa [add_comm, add_left_comm, add_assoc, sub_eq_add_neg] using
  --           add_le_add_left hk'' aâŸ©
  --   have ht_strict : StrictMono t := by
  --     intro i j hij
  --     change a + (i : â„) * Î” < a + (j : â„) * Î”
  --     have hij' : (i : â„) < (j : â„) := by exact_mod_cast Fin.lt_iff_val_lt_val.mp hij
  --     exact add_lt_add_left (mul_lt_mul_of_pos_right hij' hÎ”_pos) _
  --   have ht0 : (t 0 : â„) = a := by
  --     simp [t]
  --   have ht_last : (t (Fin.last n) : â„) = b := by
  --     have hmul : (n : â„) * Î” = b - a := by
  --       have hn_ne : (n : â„) â‰  0 := hn_ne_zero
  --       have : ((b - a) / (n : â„)) * (n : â„) = b - a := by
  --         field_simp [hn_ne]
  --       simpa [Î”, mul_comm] using this
  --     have hval : (t (Fin.last n) : â„) = a + (n : â„) * Î” := by simp [t]
  --     calc
  --       (t (Fin.last n) : â„)
  --           = a + (n : â„) * Î” := hval
  --       _ = a + (b - a) := by simp [hmul]
  --       _ = b := by simp [sub_eq_add_neg]
  --   have hsucc_diff :
  --       âˆ€ i : Fin n, (t i.succ : â„) - (t i.castSucc : â„) = Î” := by
  --     intro i
  --     have hsucc' : (t i.succ : â„) = a + (i.succ : â„) * Î” := by simp [t]
  --     have hcast' : (t i.castSucc : â„) = a + (i.castSucc : â„) * Î” := by simp [t]
  --     have hcast : (i.castSucc : â„) = (i : â„) := by simp
  --     have hisucc : (i.succ : â„) = (i : â„) + 1 := by simp
  --     have hcast'' : (t i.castSucc : â„) = a + (i : â„) * Î” := by simpa [hcast] using hcast'
  --     have hsucc'' : (t i.succ : â„) = a + ((i : â„) + 1) * Î” := by simpa [hisucc] using hsucc'
  --     have histepâ‚ : (â†‘â†‘i + 1) * Î” = (â†‘â†‘i : â„) * Î” + Î” := by
  --       simp [add_mul, add_comm]
  --     have histep : (â†‘â†‘i + 1) * Î” = Î” + (â†‘â†‘i : â„) * Î” := by
  --       simpa [add_comm] using histepâ‚
  --     have hstep :
  --         (t i.succ : â„) = (t i.castSucc : â„) + Î” := by
  --       calc
  --         (t i.succ : â„)
  --             = a + ((i : â„) + 1) * Î” := hsucc''
  --         _ = a + ((i : â„) * Î” + Î”) := by simp [histepâ‚]
  --         _ = (a + (i : â„) * Î”) + Î” := by ac_rfl
  --         _ = (t i.castSucc : â„) + Î” := by simp [hcast'']
  --     simp [hstep]
  --   have hinterval_subset :
  --       âˆ€ i : Fin n, Icc (t i.castSucc) (t i.succ) âŠ† Metric.ball (t i.castSucc) Î´ := by
  --     intro i x hx
  --     rw [Metric.mem_ball]
  --     have hx_left : (t i.castSucc : â„) â‰¤ (x : â„) := hx.1
  --     have hx_right : (x : â„) â‰¤ (t i.succ : â„) := hx.2
  --     have hx_nonneg : 0 â‰¤ (x : â„) - (t i.castSucc : â„) := sub_nonneg.mpr hx_left
  --     have hx_leÎ” : (x : â„) - (t i.castSucc : â„) â‰¤ Î” := by
  --       have := sub_le_sub_right hx_right (t i.castSucc : â„)
  --       simpa [hsucc_diff i] using this
  --     have hx_dist_le : dist x (t i.castSucc) â‰¤ Î” := by
  --       change dist (x : â„) (t i.castSucc : â„) â‰¤ Î”
  --       simpa [Real.dist_eq, abs_of_nonneg hx_nonneg] using hx_leÎ”
  --     exact (lt_of_le_of_lt hx_dist_le hÎ”_lt)
  --   have hcover :
  --       âˆ€ i : Fin n, âˆƒ j : Î¹, Icc (t i.castSucc) (t i.succ) âŠ† c j := by
  --     intro i
  --     obtain âŸ¨j, hjâŸ© := ball_subset (t i.castSucc) trivial
  --     exact âŸ¨j, (hinterval_subset i).trans hjâŸ©
  --   exact âŸ¨n, t, ht_strict, ht0, ht_last, hcoverâŸ©
  -- Â· intro h_eq
  --   subst h_eq
  --   refine âŸ¨0, fun _ : Fin 1 => âŸ¨a, by simpâŸ©, ?_, ?_, ?_, ?_âŸ©
  --   Â· intro i j hij
  --     grind
  --   Â· simp
  --   Â· simp
  --   Â· intro i
  --     exact i.elim0

/-- Finite partition variant: Any open cover of the unit interval can be refined to a finite
partition with strictly monotone partition points indexed by `Fin (n + 1)`. -/
lemma exists_strictMono_Icc_subset_open_cover_unitInterval {Î¹} {c : Î¹ â†’ Set I}
    (hcâ‚ : âˆ€ i, IsOpen (c i)) (hcâ‚‚ : univ âŠ† â‹ƒ i, c i) :
    âˆƒ (n : â„•) (t : Fin (n + 1) â†’ I),
      StrictMono t âˆ§ t 0 = 0 âˆ§ t (Fin.last n) = 1 âˆ§
      âˆ€ i : Fin n, âˆƒ j : Î¹, Icc (t i.castSucc) (t i.succ) âŠ† c j := by
  obtain âŸ¨n, t, ht_strict, ht0, htn, ht_coverâŸ© :=
    exists_strictMono_Icc_subset_open_cover_Icc zero_le_one hcâ‚ hcâ‚‚
  refine âŸ¨n, t, ht_strict, ?_, ?_, ht_coverâŸ©
  Â· ext; exact ht0
  Â· ext; exact htn

lemma exists_monotone_Icc_subset_open_cover_unitInterval_prod_self {Î¹} {c : Î¹ â†’ Set (I Ã— I)}
    (hcâ‚ : âˆ€ i, IsOpen (c i)) (hcâ‚‚ : univ âŠ† â‹ƒ i, c i) :
    âˆƒ t : â„• â†’ I, t 0 = 0 âˆ§ Monotone t âˆ§ (âˆƒ n, âˆ€ m â‰¥ n, t m = 1) âˆ§
      âˆ€ n m, âˆƒ i, Icc (t n) (t (n + 1)) Ã—Ë¢ Icc (t m) (t (m + 1)) âŠ† c i := by
  obtain âŸ¨Î´, Î´_pos, ball_subsetâŸ© := lebesgue_number_lemma_of_metric isCompact_univ hcâ‚ hcâ‚‚
  have hÎ´ := half_pos Î´_pos
  simp_rw [Subtype.ext_iff]
  have h : (0 : â„) â‰¤ 1 := zero_le_one
  refine âŸ¨addNSMul h (Î´/2), addNSMul_zero h,
    monotone_addNSMul h hÎ´.le, addNSMul_eq_right h hÎ´, fun n m â†¦ ?_âŸ©
  obtain âŸ¨i, hsubâŸ© := ball_subset (addNSMul h (Î´ / 2) n, addNSMul h (Î´ / 2) m) trivial
  exact âŸ¨i, fun t ht â†¦ hsub (Metric.mem_ball.mpr <| (max_le (abs_sub_addNSMul_le h hÎ´.le n ht.1) <|
    abs_sub_addNSMul_le h hÎ´.le m ht.2).trans_lt <| half_lt_self Î´_pos)âŸ©

end partition

@[simp]
theorem projIcc_eq_zero {x : â„} : projIcc (0 : â„) 1 zero_le_one x = 0 â†” x â‰¤ 0 :=
  projIcc_eq_left zero_lt_one

@[simp]
theorem projIcc_eq_one {x : â„} : projIcc (0 : â„) 1 zero_le_one x = 1 â†” 1 â‰¤ x :=
  projIcc_eq_right zero_lt_one

namespace Tactic.Interactive

/-- A tactic that solves `0 â‰¤ â†‘x`, `0 â‰¤ 1 - â†‘x`, `â†‘x â‰¤ 1`, and `1 - â†‘x â‰¤ 1` for `x : I`. -/
macro "unit_interval" : tactic =>
  `(tactic| (first
  | apply unitInterval.nonneg
  | apply unitInterval.one_minus_nonneg
  | apply unitInterval.le_one
  | apply unitInterval.one_minus_le_one))

example (x : unitInterval) : 0 â‰¤ (x : â„) := by unit_interval

end Tactic.Interactive

section

variable {ğ•œ : Type*} [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
  [TopologicalSpace ğ•œ] [IsTopologicalRing ğ•œ]

-- We only need the ordering on `ğ•œ` here to avoid talking about flipping the interval over.
-- At the end of the day I only care about `â„`, so I'm hesitant to put work into generalizing.
/-- The image of `[0,1]` under the homeomorphism `fun x â†¦ a * x + b` is `[b, a+b]`.
-/
theorem affineHomeomorph_image_I (a b : ğ•œ) (h : 0 < a) :
    affineHomeomorph a b h.ne.symm '' Set.Icc 0 1 = Set.Icc b (a + b) := by simp [h]

/-- The affine homeomorphism from a nontrivial interval `[a,b]` to `[0,1]`.
-/
def iccHomeoI (a b : ğ•œ) (h : a < b) : Set.Icc a b â‰ƒâ‚œ Set.Icc (0 : ğ•œ) (1 : ğ•œ) := by
  let e := Homeomorph.image (affineHomeomorph (b - a) a (sub_pos.mpr h).ne.symm) (Set.Icc 0 1)
  refine (e.trans ?_).symm
  apply Homeomorph.setCongr
  rw [affineHomeomorph_image_I _ _ (sub_pos.2 h)]
  simp

@[simp]
theorem iccHomeoI_apply_coe (a b : ğ•œ) (h : a < b) (x : Set.Icc a b) :
    ((iccHomeoI a b h) x : ğ•œ) = (x - a) / (b - a) :=
  rfl

@[simp]
theorem iccHomeoI_symm_apply_coe (a b : ğ•œ) (h : a < b) (x : Set.Icc (0 : ğ•œ) (1 : ğ•œ)) :
    ((iccHomeoI a b h).symm x : ğ•œ) = (b - a) * x + a :=
  rfl

end

namespace unitInterval

open NNReal

/-- The coercion from `I` to `â„â‰¥0`. -/
def toNNReal : I â†’ â„â‰¥0 := fun i â†¦ âŸ¨i.1, i.2.1âŸ©

@[simp] lemma toNNReal_zero : toNNReal 0 = 0 := rfl
@[simp] lemma toNNReal_one : toNNReal 1 = 1 := rfl

@[fun_prop] lemma toNNReal_continuous : Continuous toNNReal := by delta toNNReal; fun_prop

@[simp] lemma coe_toNNReal (x : I) : ((toNNReal x) : â„) = x := rfl

@[simp] lemma toNNReal_add_toNNReal_symm (x : I) : toNNReal x + toNNReal (Ïƒ x) = 1 := by ext; simp
@[simp] lemma toNNReal_symm_add_toNNReal (x : I) : toNNReal (Ïƒ x) + toNNReal x = 1 := by ext; simp

end unitInterval
