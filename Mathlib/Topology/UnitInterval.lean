/-
Copyright (c) 2020 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Scott Morrison
-/
import Mathlib.Topology.Instances.Real
import Mathlib.Topology.Algebra.Field
import Mathlib.Data.Set.Intervals.ProjIcc
import Mathlib.Data.Set.Intervals.Instances

#align_import topology.unit_interval from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# The unit interval, as a topological space

Use `open unitInterval` to turn on the notation `I := Set.Icc (0 : â„) (1 : â„)`.

We provide basic instances, as well as a custom tactic for discharging
`0 â‰¤ â†‘x`, `0 â‰¤ 1 - â†‘x`, `â†‘x â‰¤ 1`, and `1 - â†‘x â‰¤ 1` when `x : I`.

-/

noncomputable section

open Classical Topology Filter

open Set Int Set.Icc

/-! ### The unit interval -/


/-- The unit interval `[0,1]` in â„. -/
abbrev unitInterval : Set â„ :=
  Set.Icc 0 1
#align unit_interval unitInterval

@[inherit_doc]
scoped[unitInterval] notation "I" => unitInterval

namespace unitInterval

theorem zero_mem : (0 : â„) âˆˆ I :=
  âŸ¨le_rfl, zero_le_oneâŸ©
#align unit_interval.zero_mem unitInterval.zero_mem

theorem one_mem : (1 : â„) âˆˆ I :=
  âŸ¨zero_le_one, le_rflâŸ©
#align unit_interval.one_mem unitInterval.one_mem

theorem mul_mem {x y : â„} (hx : x âˆˆ I) (hy : y âˆˆ I) : x * y âˆˆ I :=
  âŸ¨mul_nonneg hx.1 hy.1, (mul_le_mul hx.2 hy.2 hy.1 zero_le_one).trans_eq <| one_mul 1âŸ©
#align unit_interval.mul_mem unitInterval.mul_mem

theorem div_mem {x y : â„} (hx : 0 â‰¤ x) (hy : 0 â‰¤ y) (hxy : x â‰¤ y) : x / y âˆˆ I :=
  âŸ¨div_nonneg hx hy, div_le_one_of_le hxy hyâŸ©
#align unit_interval.div_mem unitInterval.div_mem

theorem fract_mem (x : â„) : fract x âˆˆ I :=
  âŸ¨fract_nonneg _, (fract_lt_one _).leâŸ©
#align unit_interval.fract_mem unitInterval.fract_mem

theorem mem_iff_one_sub_mem {t : â„} : t âˆˆ I â†” 1 - t âˆˆ I := by
  rw [mem_Icc, mem_Icc]
  constructor <;> intro <;> constructor <;> linarith
#align unit_interval.mem_iff_one_sub_mem unitInterval.mem_iff_one_sub_mem

instance hasZero : Zero I :=
  âŸ¨âŸ¨0, zero_memâŸ©âŸ©
#align unit_interval.has_zero unitInterval.hasZero

instance hasOne : One I :=
  âŸ¨âŸ¨1, by constructor <;> norm_numâŸ©âŸ©
#align unit_interval.has_one unitInterval.hasOne

instance : ZeroLEOneClass I := âŸ¨@zero_le_one â„ _ _ _ _âŸ©

instance : BoundedOrder I := Set.Icc.boundedOrder zero_le_one

lemma univ_eq_Icc : (univ : Set I) = Icc 0 1 := by
  ext âŸ¨t, t0, t1âŸ©; simp_rw [mem_univ, true_iff]; exact âŸ¨t0, t1âŸ©

theorem coe_ne_zero {x : I} : (x : â„) â‰  0 â†” x â‰  0 :=
  not_iff_not.mpr coe_eq_zero
#align unit_interval.coe_ne_zero unitInterval.coe_ne_zero

theorem coe_ne_one {x : I} : (x : â„) â‰  1 â†” x â‰  1 :=
  not_iff_not.mpr coe_eq_one
#align unit_interval.coe_ne_one unitInterval.coe_ne_one

instance : Nonempty I :=
  âŸ¨0âŸ©

instance : Mul I :=
  âŸ¨fun x y => âŸ¨x * y, mul_mem x.2 y.2âŸ©âŸ©

-- todo: we could set up a `LinearOrderedCommMonoidWithZero I` instance
theorem mul_le_left {x y : I} : x * y â‰¤ x :=
  Subtype.coe_le_coe.mp <| (mul_le_mul_of_nonneg_left y.2.2 x.2.1).trans_eq <| mul_one x.1
#align unit_interval.mul_le_left unitInterval.mul_le_left

theorem mul_le_right {x y : I} : x * y â‰¤ y :=
  Subtype.coe_le_coe.mp <| (mul_le_mul_of_nonneg_right x.2.2 y.2.1).trans_eq <| one_mul y.1
#align unit_interval.mul_le_right unitInterval.mul_le_right

/-- Unit interval central symmetry. -/
def symm : I â†’ I := fun t => âŸ¨1 - t, mem_iff_one_sub_mem.mp t.propâŸ©
#align unit_interval.symm unitInterval.symm

@[inherit_doc]
scoped notation "Ïƒ" => unitInterval.symm

@[simp]
theorem symm_zero : Ïƒ 0 = 1 :=
  Subtype.ext <| by simp [symm]
#align unit_interval.symm_zero unitInterval.symm_zero

@[simp]
theorem symm_one : Ïƒ 1 = 0 :=
  Subtype.ext <| by simp [symm]
#align unit_interval.symm_one unitInterval.symm_one

@[simp]
theorem symm_symm (x : I) : Ïƒ (Ïƒ x) = x :=
  Subtype.ext <| by simp [symm]
#align unit_interval.symm_symm unitInterval.symm_symm

@[simp]
theorem coe_symm_eq (x : I) : (Ïƒ x : â„) = 1 - x :=
  rfl
#align unit_interval.coe_symm_eq unitInterval.coe_symm_eq

-- Porting note: Proof used to be `by continuity!`
@[continuity]
theorem continuous_symm : Continuous Ïƒ :=
  (continuous_const.add continuous_induced_dom.neg).subtype_mk _
#align unit_interval.continuous_symm unitInterval.continuous_symm

lemma strictAnti_symm : StrictAnti Ïƒ := fun _ _ h â†¦ sub_lt_sub_left (Î± := â„) h _

lemma bijective_symm : Function.Bijective Ïƒ :=
  Function.bijective_iff_has_inverse.mpr âŸ¨_, symm_symm, symm_symmâŸ©

lemma half_le_symm_iff (t : I) : 1 / 2 â‰¤ (Ïƒ t : â„) â†” (t : â„) â‰¤ 1 / 2 := by
  rw [coe_symm_eq, le_sub_iff_add_le, add_comm, â†le_sub_iff_add_le, sub_half]

instance : ConnectedSpace I :=
  Subtype.connectedSpace âŸ¨nonempty_Icc.mpr zero_le_one, isPreconnected_IccâŸ©

/-- Verify there is an instance for `CompactSpace I`. -/
example : CompactSpace I := by infer_instance

theorem nonneg (x : I) : 0 â‰¤ (x : â„) :=
  x.2.1
#align unit_interval.nonneg unitInterval.nonneg

theorem one_minus_nonneg (x : I) : 0 â‰¤ 1 - (x : â„) := by simpa using x.2.2
#align unit_interval.one_minus_nonneg unitInterval.one_minus_nonneg

theorem le_one (x : I) : (x : â„) â‰¤ 1 :=
  x.2.2
#align unit_interval.le_one unitInterval.le_one

theorem one_minus_le_one (x : I) : 1 - (x : â„) â‰¤ 1 := by simpa using x.2.1
#align unit_interval.one_minus_le_one unitInterval.one_minus_le_one

theorem add_pos {t : I} {x : â„} (hx : 0 < x) : 0 < (x + t : â„) :=
  add_pos_of_pos_of_nonneg hx <| nonneg _
#align unit_interval.add_pos unitInterval.add_pos

/-- like `unitInterval.nonneg`, but with the inequality in `I`. -/
theorem nonneg' {t : I} : 0 â‰¤ t :=
  t.2.1
#align unit_interval.nonneg' unitInterval.nonneg'

/-- like `unitInterval.le_one`, but with the inequality in `I`. -/
theorem le_one' {t : I} : t â‰¤ 1 :=
  t.2.2
#align unit_interval.le_one' unitInterval.le_one'

instance : NeZero (1 : I) := âŸ¨fun h â†¦ one_ne_zero <| congrArg Subtype.val hâŸ©

theorem mul_pos_mem_iff {a t : â„} (ha : 0 < a) : a * t âˆˆ I â†” t âˆˆ Set.Icc (0 : â„) (1 / a) := by
  constructor <;> rintro âŸ¨hâ‚, hâ‚‚âŸ© <;> constructor
  Â· exact nonneg_of_mul_nonneg_right hâ‚ ha
  Â· rwa [le_div_iff ha, mul_comm]
  Â· exact mul_nonneg ha.le hâ‚
  Â· rwa [le_div_iff ha, mul_comm] at hâ‚‚
#align unit_interval.mul_pos_mem_iff unitInterval.mul_pos_mem_iff

theorem two_mul_sub_one_mem_iff {t : â„} : 2 * t - 1 âˆˆ I â†” t âˆˆ Set.Icc (1 / 2 : â„) 1 := by
  constructor <;> rintro âŸ¨hâ‚, hâ‚‚âŸ© <;> constructor <;> linarith
#align unit_interval.two_mul_sub_one_mem_iff unitInterval.two_mul_sub_one_mem_iff

end unitInterval

section partition

namespace Set.Icc

variable {Î±} [LinearOrderedAddCommGroup Î±] {a b c d : Î±} (h : a â‰¤ b) {Î´ : Î±}

-- TODO: Set.projIci, Set.projIic
/-- `Set.projIcc` is a contraction. -/
lemma _root_.Set.abs_projIcc_sub_projIcc : (|projIcc a b h c - projIcc a b h d| : Î±) â‰¤ |c - d| := by
  wlog hdc : d â‰¤ c generalizing c d
  Â· rw [abs_sub_comm, abs_sub_comm c]; exact this (le_of_not_le hdc)
  rw [abs_eq_self.2 (sub_nonneg.2 hdc), abs_eq_self.2 (sub_nonneg.2 <| monotone_projIcc h hdc)]
  rw [â† sub_nonneg] at hdc
  refine (max_sub_max_le_max _ _ _ _).trans (max_le (by rwa [sub_self]) ?_)
  refine ((le_abs_self _).trans <| abs_min_sub_min_le_max _ _ _ _).trans (max_le ?_ ?_)
  Â· rwa [sub_self, abs_zero]
  Â· exact (abs_eq_self.mpr hdc).le

/-- Equally spaced points in a closed interval. -/
def addNsmul (Î´ : Î±) (n : â„•) : Icc a b := projIcc a b h (a + n â€¢ Î´)

lemma addNsmul_zero : addNsmul h Î´ 0 = a := by
  rw [addNsmul, zero_smul, add_zero, projIcc_left]

lemma addNsmul_eq_right [Archimedean Î±] (hÎ´ : 0 < Î´) :
    âˆƒ m, âˆ€ n â‰¥ m, addNsmul h Î´ n = b := by
  obtain âŸ¨m, hmâŸ© := Archimedean.arch (b - a) hÎ´
  refine âŸ¨m, fun n hn â†¦ ?_âŸ©
  rw [addNsmul, coe_projIcc, add_comm, min_eq_left_iff.mpr, max_eq_right h]
  exact sub_le_iff_le_add.mp (hm.trans <| nsmul_le_nsmul hÎ´.le hn)

lemma monotone_addNsmul (hÎ´ : 0 â‰¤ Î´) : Monotone (addNsmul h Î´) :=
  fun _ _ hnm â†¦ monotone_projIcc h <| (add_le_add_iff_left _).mpr (nsmul_le_nsmul hÎ´ hnm)

lemma abs_sub_addNsmul_le (hÎ´ : 0 â‰¤ Î´) {t : Icc a b} (n : â„•)
    (ht : t âˆˆ Icc (addNsmul h Î´ n) (addNsmul h Î´ (n+1))) :
    (|t - addNsmul h Î´ n| : Î±) â‰¤ Î´ :=
  (abs_eq_self.2 <| sub_nonneg.2 ht.1).trans_le <| (sub_le_sub_right (by exact ht.2) _).trans <|
    (le_abs_self _).trans <| (abs_projIcc_sub_projIcc h).trans <| by
      rw [add_sub_add_comm, sub_self, zero_add, succ_nsmul, add_sub_cancel]
      exact (abs_eq_self.mpr hÎ´).le

end Set.Icc

open scoped unitInterval

/-- Any open cover of a closed interval in â„ can be refined to
  a finite partition into subintervals. -/
lemma lebesgue_number_lemma_Icc {Î¹} {a b : â„} (h : a â‰¤ b) {c : Î¹ â†’ Set (Icc a b)}
    (hcâ‚ : âˆ€ i, IsOpen (c i)) (hcâ‚‚ : univ âŠ† â‹ƒ i, c i) : âˆƒ t : â„• â†’ Icc a b, t 0 = a âˆ§
      Monotone t âˆ§ (âˆƒ m, âˆ€ n â‰¥ m, t n = b) âˆ§ âˆ€ n, âˆƒ i, Icc (t n) (t (n + 1)) âŠ† c i := by
  obtain âŸ¨Î´, Î´_pos, ball_subsetâŸ© := lebesgue_number_lemma_of_metric isCompact_univ hcâ‚ hcâ‚‚
  have hÎ´ := half_pos Î´_pos
  refine âŸ¨addNsmul h (Î´/2), addNsmul_zero h,
    monotone_addNsmul h hÎ´.le, addNsmul_eq_right h hÎ´, fun n â†¦ ?_âŸ©
  obtain âŸ¨i, hsubâŸ© := ball_subset (addNsmul h (Î´/2) n) trivial
  exact âŸ¨i, fun t ht â†¦ hsub ((abs_sub_addNsmul_le h hÎ´.le n ht).trans_lt <| half_lt_self Î´_pos)âŸ©

/-- Any open cover of the unit interval can be refined to a finite partition into subintervals. -/
lemma lebesgue_number_lemma_unitInterval {Î¹} {c : Î¹ â†’ Set I} (hcâ‚ : âˆ€ i, IsOpen (c i))
    (hcâ‚‚ : univ âŠ† â‹ƒ i, c i) : âˆƒ t : â„• â†’ I, t 0 = 0 âˆ§ Monotone t âˆ§
      (âˆƒ n, âˆ€ m â‰¥ n, t m = 1) âˆ§ âˆ€ n, âˆƒ i, Icc (t n) (t (n + 1)) âŠ† c i := by
  simp_rw [â† Subtype.coe_inj]
  exact lebesgue_number_lemma_Icc zero_le_one hcâ‚ hcâ‚‚

lemma lebesgue_number_lemma_unitInterval_prod_self {Î¹} {c : Î¹ â†’ Set (I Ã— I)}
    (hcâ‚ : âˆ€ i, IsOpen (c i)) (hcâ‚‚ : univ âŠ† â‹ƒ i, c i) :
    âˆƒ t : â„• â†’ I, t 0 = 0 âˆ§ Monotone t âˆ§ (âˆƒ n, âˆ€ m â‰¥ n, t m = 1) âˆ§
      âˆ€ n m, âˆƒ i, Icc (t n) (t (n + 1)) Ã—Ë¢ Icc (t m) (t (m + 1)) âŠ† c i := by
  obtain âŸ¨Î´, Î´_pos, ball_subsetâŸ© := lebesgue_number_lemma_of_metric isCompact_univ hcâ‚ hcâ‚‚
  have hÎ´ := half_pos Î´_pos
  simp_rw [Subtype.ext_iff]
  have h : (0 : â„) â‰¤ 1 := zero_le_one
  refine âŸ¨addNsmul h (Î´/2), addNsmul_zero h,
    monotone_addNsmul h hÎ´.le, addNsmul_eq_right h hÎ´, fun n m â†¦ ?_âŸ©
  obtain âŸ¨i, hsubâŸ© := ball_subset (addNsmul h (Î´/2) n, addNsmul h (Î´/2) m) trivial
  exact âŸ¨i, fun t ht â†¦ hsub (Metric.mem_ball.mpr <| (max_le (abs_sub_addNsmul_le h hÎ´.le n ht.1) <|
    abs_sub_addNsmul_le h hÎ´.le m ht.2).trans_lt <| half_lt_self Î´_pos)âŸ©

end partition

@[simp]
theorem projIcc_eq_zero {x : â„} : projIcc (0 : â„) 1 zero_le_one x = 0 â†” x â‰¤ 0 :=
  projIcc_eq_left zero_lt_one
#align proj_Icc_eq_zero projIcc_eq_zero

@[simp]
theorem projIcc_eq_one {x : â„} : projIcc (0 : â„) 1 zero_le_one x = 1 â†” 1 â‰¤ x :=
  projIcc_eq_right zero_lt_one
#align proj_Icc_eq_one projIcc_eq_one

namespace Tactic.Interactive

-- Porting note: This replaces an unsafe def tactic
/-- A tactic that solves `0 â‰¤ â†‘x`, `0 â‰¤ 1 - â†‘x`, `â†‘x â‰¤ 1`, and `1 - â†‘x â‰¤ 1` for `x : I`. -/
macro "unit_interval" : tactic =>
  `(tactic| (first
  | apply unitInterval.nonneg
  | apply unitInterval.one_minus_nonneg
  | apply unitInterval.le_one
  | apply unitInterval.one_minus_le_one))
#noalign tactic.interactive.unit_interval

example (x : unitInterval) : 0 â‰¤ (x : â„) := by unit_interval

end Tactic.Interactive

section

variable {ğ•œ : Type*} [LinearOrderedField ğ•œ] [TopologicalSpace ğ•œ] [TopologicalRing ğ•œ]

-- We only need the ordering on `ğ•œ` here to avoid talking about flipping the interval over.
-- At the end of the day I only care about `â„`, so I'm hesitant to put work into generalizing.
/-- The image of `[0,1]` under the homeomorphism `fun x â†¦ a * x + b` is `[b, a+b]`.
-/
theorem affineHomeomorph_image_I (a b : ğ•œ) (h : 0 < a) :
    affineHomeomorph a b h.ne.symm '' Set.Icc 0 1 = Set.Icc b (a + b) := by simp [h]
set_option linter.uppercaseLean3 false in
#align affine_homeomorph_image_I affineHomeomorph_image_I

/-- The affine homeomorphism from a nontrivial interval `[a,b]` to `[0,1]`.
-/
def iccHomeoI (a b : ğ•œ) (h : a < b) : Set.Icc a b â‰ƒâ‚œ Set.Icc (0 : ğ•œ) (1 : ğ•œ) := by
  let e := Homeomorph.image (affineHomeomorph (b - a) a (sub_pos.mpr h).ne.symm) (Set.Icc 0 1)
  refine' (e.trans _).symm
  apply Homeomorph.setCongr
  rw [affineHomeomorph_image_I _ _ (sub_pos.2 h)]
  simp
set_option linter.uppercaseLean3 false in
#align Icc_homeo_I iccHomeoI

@[simp]
theorem iccHomeoI_apply_coe (a b : ğ•œ) (h : a < b) (x : Set.Icc a b) :
    ((iccHomeoI a b h) x : ğ•œ) = (x - a) / (b - a) :=
  rfl
set_option linter.uppercaseLean3 false in
#align Icc_homeo_I_apply_coe iccHomeoI_apply_coe

@[simp]
theorem iccHomeoI_symm_apply_coe (a b : ğ•œ) (h : a < b) (x : Set.Icc (0 : ğ•œ) (1 : ğ•œ)) :
    ((iccHomeoI a b h).symm x : ğ•œ) = (b - a) * x + a :=
  rfl
set_option linter.uppercaseLean3 false in
#align Icc_homeo_I_symm_apply_coe iccHomeoI_symm_apply_coe

end
