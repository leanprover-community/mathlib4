/-
Copyright (c) 2020 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Scott Morrison
-/
import Mathlib.Algebra.Order.Interval.Set.Instances
import Mathlib.Order.Interval.Set.ProjIcc
import Mathlib.Topology.Instances.Real

#align_import topology.unit_interval from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# The unit interval, as a topological space

Use `open unitInterval` to turn on the notation `I := Set.Icc (0 : ‚Ñù) (1 : ‚Ñù)`.

We provide basic instances, as well as a custom tactic for discharging
`0 ‚â§ ‚Üëx`, `0 ‚â§ 1 - ‚Üëx`, `‚Üëx ‚â§ 1`, and `1 - ‚Üëx ‚â§ 1` when `x : I`.

-/

noncomputable section

open scoped Classical
open Topology Filter

open Set Int Set.Icc

/-! ### The unit interval -/


/-- The unit interval `[0,1]` in ‚Ñù. -/
abbrev unitInterval : Set ‚Ñù :=
  Set.Icc 0 1
#align unit_interval unitInterval

@[inherit_doc]
scoped[unitInterval] notation "I" => unitInterval

namespace unitInterval

theorem zero_mem : (0 : ‚Ñù) ‚àà I :=
  ‚ü®le_rfl, zero_le_one‚ü©
#align unit_interval.zero_mem unitInterval.zero_mem

theorem one_mem : (1 : ‚Ñù) ‚àà I :=
  ‚ü®zero_le_one, le_rfl‚ü©
#align unit_interval.one_mem unitInterval.one_mem

theorem mul_mem {x y : ‚Ñù} (hx : x ‚àà I) (hy : y ‚àà I) : x * y ‚àà I :=
  ‚ü®mul_nonneg hx.1 hy.1, mul_le_one hx.2 hy.1 hy.2‚ü©
#align unit_interval.mul_mem unitInterval.mul_mem

theorem div_mem {x y : ‚Ñù} (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) (hxy : x ‚â§ y) : x / y ‚àà I :=
  ‚ü®div_nonneg hx hy, div_le_one_of_le hxy hy‚ü©
#align unit_interval.div_mem unitInterval.div_mem

theorem fract_mem (x : ‚Ñù) : fract x ‚àà I :=
  ‚ü®fract_nonneg _, (fract_lt_one _).le‚ü©
#align unit_interval.fract_mem unitInterval.fract_mem

theorem mem_iff_one_sub_mem {t : ‚Ñù} : t ‚àà I ‚Üî 1 - t ‚àà I := by
  rw [mem_Icc, mem_Icc]
  constructor <;> intro <;> constructor <;> linarith
#align unit_interval.mem_iff_one_sub_mem unitInterval.mem_iff_one_sub_mem

instance hasZero : Zero I :=
  ‚ü®‚ü®0, zero_mem‚ü©‚ü©
#align unit_interval.has_zero unitInterval.hasZero

instance hasOne : One I :=
  ‚ü®‚ü®1, by constructor <;> norm_num‚ü©‚ü©
#align unit_interval.has_one unitInterval.hasOne

instance : ZeroLEOneClass I := ‚ü®zero_le_one (Œ± := ‚Ñù)‚ü©

instance : BoundedOrder I := Set.Icc.boundedOrder zero_le_one

lemma univ_eq_Icc : (univ : Set I) = Icc (0 : I) (1 : I) := Icc_bot_top.symm

theorem coe_ne_zero {x : I} : (x : ‚Ñù) ‚â† 0 ‚Üî x ‚â† 0 :=
  not_iff_not.mpr coe_eq_zero
#align unit_interval.coe_ne_zero unitInterval.coe_ne_zero

theorem coe_ne_one {x : I} : (x : ‚Ñù) ‚â† 1 ‚Üî x ‚â† 1 :=
  not_iff_not.mpr coe_eq_one
#align unit_interval.coe_ne_one unitInterval.coe_ne_one

instance : Nonempty I :=
  ‚ü®0‚ü©

instance : Mul I :=
  ‚ü®fun x y => ‚ü®x * y, mul_mem x.2 y.2‚ü©‚ü©

-- todo: we could set up a `LinearOrderedCommMonoidWithZero I` instance
theorem mul_le_left {x y : I} : x * y ‚â§ x :=
  Subtype.coe_le_coe.mp <| mul_le_of_le_one_right x.2.1 y.2.2
#align unit_interval.mul_le_left unitInterval.mul_le_left

theorem mul_le_right {x y : I} : x * y ‚â§ y :=
  Subtype.coe_le_coe.mp <| mul_le_of_le_one_left y.2.1 x.2.2
#align unit_interval.mul_le_right unitInterval.mul_le_right

/-- Unit interval central symmetry. -/
def symm : I ‚Üí I := fun t => ‚ü®1 - t, mem_iff_one_sub_mem.mp t.prop‚ü©
#align unit_interval.symm unitInterval.symm

@[inherit_doc]
scoped notation "œÉ" => unitInterval.symm

@[simp]
theorem symm_zero : œÉ 0 = 1 :=
  Subtype.ext <| by simp [symm]
#align unit_interval.symm_zero unitInterval.symm_zero

@[simp]
theorem symm_one : œÉ 1 = 0 :=
  Subtype.ext <| by simp [symm]
#align unit_interval.symm_one unitInterval.symm_one

@[simp]
theorem symm_symm (x : I) : œÉ (œÉ x) = x :=
  Subtype.ext <| by simp [symm]
#align unit_interval.symm_symm unitInterval.symm_symm

theorem symm_involutive : Function.Involutive (symm : I ‚Üí I) := symm_symm

theorem symm_bijective : Function.Bijective (symm : I ‚Üí I) := symm_involutive.bijective

@[simp]
theorem coe_symm_eq (x : I) : (œÉ x : ‚Ñù) = 1 - x :=
  rfl
#align unit_interval.coe_symm_eq unitInterval.coe_symm_eq

-- Porting note: Proof used to be `by continuity!`
@[continuity]
theorem continuous_symm : Continuous œÉ :=
  (continuous_const.add continuous_induced_dom.neg).subtype_mk _
#align unit_interval.continuous_symm unitInterval.continuous_symm

/-- `unitInterval.symm` as a `Homeomorph`. -/
@[simps]
def symmHomeomorph : I ‚âÉ‚Çú I where
  toFun := symm
  invFun := symm
  left_inv := symm_symm
  right_inv := symm_symm

theorem strictAnti_symm : StrictAnti œÉ := fun _ _ h ‚Ü¶ sub_lt_sub_left (Œ± := ‚Ñù) h _

-- 2024-02-27
@[deprecated] alias involutive_symm := symm_involutive

-- 2024-02-27
@[deprecated] alias bijective_symm := symm_bijective

theorem half_le_symm_iff (t : I) : 1 / 2 ‚â§ (œÉ t : ‚Ñù) ‚Üî (t : ‚Ñù) ‚â§ 1 / 2 := by
  rw [coe_symm_eq, le_sub_iff_add_le, add_comm, ‚Üê le_sub_iff_add_le, sub_half]

instance : ConnectedSpace I :=
  Subtype.connectedSpace ‚ü®nonempty_Icc.mpr zero_le_one, isPreconnected_Icc‚ü©

/-- Verify there is an instance for `CompactSpace I`. -/
example : CompactSpace I := by infer_instance

theorem nonneg (x : I) : 0 ‚â§ (x : ‚Ñù) :=
  x.2.1
#align unit_interval.nonneg unitInterval.nonneg

theorem one_minus_nonneg (x : I) : 0 ‚â§ 1 - (x : ‚Ñù) := by simpa using x.2.2
#align unit_interval.one_minus_nonneg unitInterval.one_minus_nonneg

theorem le_one (x : I) : (x : ‚Ñù) ‚â§ 1 :=
  x.2.2
#align unit_interval.le_one unitInterval.le_one

theorem one_minus_le_one (x : I) : 1 - (x : ‚Ñù) ‚â§ 1 := by simpa using x.2.1
#align unit_interval.one_minus_le_one unitInterval.one_minus_le_one

theorem add_pos {t : I} {x : ‚Ñù} (hx : 0 < x) : 0 < (x + t : ‚Ñù) :=
  add_pos_of_pos_of_nonneg hx <| nonneg _
#align unit_interval.add_pos unitInterval.add_pos

/-- like `unitInterval.nonneg`, but with the inequality in `I`. -/
theorem nonneg' {t : I} : 0 ‚â§ t :=
  t.2.1
#align unit_interval.nonneg' unitInterval.nonneg'

/-- like `unitInterval.le_one`, but with the inequality in `I`. -/
theorem le_one' {t : I} : t ‚â§ 1 :=
  t.2.2
#align unit_interval.le_one' unitInterval.le_one'

instance : Nontrivial I := ‚ü®‚ü®1, 0, (one_ne_zero <| congrArg Subtype.val ¬∑)‚ü©‚ü©

theorem mul_pos_mem_iff {a t : ‚Ñù} (ha : 0 < a) : a * t ‚àà I ‚Üî t ‚àà Set.Icc (0 : ‚Ñù) (1 / a) := by
  constructor <;> rintro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© <;> constructor
  ¬∑ exact nonneg_of_mul_nonneg_right h‚ÇÅ ha
  ¬∑ rwa [le_div_iff ha, mul_comm]
  ¬∑ exact mul_nonneg ha.le h‚ÇÅ
  ¬∑ rwa [le_div_iff ha, mul_comm] at h‚ÇÇ
#align unit_interval.mul_pos_mem_iff unitInterval.mul_pos_mem_iff

theorem two_mul_sub_one_mem_iff {t : ‚Ñù} : 2 * t - 1 ‚àà I ‚Üî t ‚àà Set.Icc (1 / 2 : ‚Ñù) 1 := by
  constructor <;> rintro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© <;> constructor <;> linarith
#align unit_interval.two_mul_sub_one_mem_iff unitInterval.two_mul_sub_one_mem_iff

end unitInterval

section partition

namespace Set.Icc

variable {Œ±} [LinearOrderedAddCommGroup Œ±] {a b c d : Œ±} (h : a ‚â§ b) {Œ¥ : Œ±}

-- TODO: Set.projIci, Set.projIic
/-- `Set.projIcc` is a contraction. -/
lemma _root_.Set.abs_projIcc_sub_projIcc : (|projIcc a b h c - projIcc a b h d| : Œ±) ‚â§ |c - d| := by
  wlog hdc : d ‚â§ c generalizing c d
  ¬∑ rw [abs_sub_comm, abs_sub_comm c]; exact this (le_of_not_le hdc)
  rw [abs_eq_self.2 (sub_nonneg.2 hdc), abs_eq_self.2 (sub_nonneg.2 <| monotone_projIcc h hdc)]
  rw [‚Üê sub_nonneg] at hdc
  refine (max_sub_max_le_max _ _ _ _).trans (max_le (by rwa [sub_self]) ?_)
  refine ((le_abs_self _).trans <| abs_min_sub_min_le_max _ _ _ _).trans (max_le ?_ ?_)
  ¬∑ rwa [sub_self, abs_zero]
  ¬∑ exact (abs_eq_self.mpr hdc).le

/-- When `h : a ‚â§ b` and `Œ¥ > 0`, `addNSMul h Œ¥` is a sequence of points in the closed interval
  `[a,b]`, which is initially equally spaced but eventually stays at the right endpoint `b`. -/
def addNSMul (Œ¥ : Œ±) (n : ‚Ñï) : Icc a b := projIcc a b h (a + n ‚Ä¢ Œ¥)

lemma addNSMul_zero : addNSMul h Œ¥ 0 = a := by
  rw [addNSMul, zero_smul, add_zero, projIcc_left]

lemma addNSMul_eq_right [Archimedean Œ±] (hŒ¥ : 0 < Œ¥) :
    ‚àÉ m, ‚àÄ n ‚â• m, addNSMul h Œ¥ n = b := by
  obtain ‚ü®m, hm‚ü© := Archimedean.arch (b - a) hŒ¥
  refine ‚ü®m, fun n hn ‚Ü¶ ?_‚ü©
  rw [addNSMul, coe_projIcc, add_comm, min_eq_left_iff.mpr, max_eq_right h]
  exact sub_le_iff_le_add.mp (hm.trans <| nsmul_le_nsmul_left hŒ¥.le hn)

lemma monotone_addNSMul (hŒ¥ : 0 ‚â§ Œ¥) : Monotone (addNSMul h Œ¥) :=
  fun _ _ hnm ‚Ü¶ monotone_projIcc h <| (add_le_add_iff_left _).mpr (nsmul_le_nsmul_left hŒ¥ hnm)

lemma abs_sub_addNSMul_le (hŒ¥ : 0 ‚â§ Œ¥) {t : Icc a b} (n : ‚Ñï)
    (ht : t ‚àà Icc (addNSMul h Œ¥ n) (addNSMul h Œ¥ (n+1))) :
    (|t - addNSMul h Œ¥ n| : Œ±) ‚â§ Œ¥ :=
  (abs_eq_self.2 <| sub_nonneg.2 ht.1).trans_le <| (sub_le_sub_right (by exact ht.2) _).trans <|
    (le_abs_self _).trans <| (abs_projIcc_sub_projIcc h).trans <| by
      rw [add_sub_add_comm, sub_self, zero_add, succ_nsmul', add_sub_cancel_right]
      exact (abs_eq_self.mpr hŒ¥).le

end Set.Icc

open scoped unitInterval

/-- Any open cover `c` of a closed interval `[a, b]` in ‚Ñù can be refined to
  a finite partition into subintervals. -/
lemma exists_monotone_Icc_subset_open_cover_Icc {Œπ} {a b : ‚Ñù} (h : a ‚â§ b) {c : Œπ ‚Üí Set (Icc a b)}
    (hc‚ÇÅ : ‚àÄ i, IsOpen (c i)) (hc‚ÇÇ : univ ‚äÜ ‚ãÉ i, c i) : ‚àÉ t : ‚Ñï ‚Üí Icc a b, t 0 = a ‚àß
      Monotone t ‚àß (‚àÉ m, ‚àÄ n ‚â• m, t n = b) ‚àß ‚àÄ n, ‚àÉ i, Icc (t n) (t (n + 1)) ‚äÜ c i := by
  obtain ‚ü®Œ¥, Œ¥_pos, ball_subset‚ü© := lebesgue_number_lemma_of_metric isCompact_univ hc‚ÇÅ hc‚ÇÇ
  have hŒ¥ := half_pos Œ¥_pos
  refine ‚ü®addNSMul h (Œ¥/2), addNSMul_zero h,
    monotone_addNSMul h hŒ¥.le, addNSMul_eq_right h hŒ¥, fun n ‚Ü¶ ?_‚ü©
  obtain ‚ü®i, hsub‚ü© := ball_subset (addNSMul h (Œ¥/2) n) trivial
  exact ‚ü®i, fun t ht ‚Ü¶ hsub ((abs_sub_addNSMul_le h hŒ¥.le n ht).trans_lt <| half_lt_self Œ¥_pos)‚ü©

/-- Any open cover of the unit interval can be refined to a finite partition into subintervals. -/
lemma exists_monotone_Icc_subset_open_cover_unitInterval {Œπ} {c : Œπ ‚Üí Set I}
    (hc‚ÇÅ : ‚àÄ i, IsOpen (c i)) (hc‚ÇÇ : univ ‚äÜ ‚ãÉ i, c i) : ‚àÉ t : ‚Ñï ‚Üí I, t 0 = 0 ‚àß
      Monotone t ‚àß (‚àÉ n, ‚àÄ m ‚â• n, t m = 1) ‚àß ‚àÄ n, ‚àÉ i, Icc (t n) (t (n + 1)) ‚äÜ c i := by
  simp_rw [‚Üê Subtype.coe_inj]
  exact exists_monotone_Icc_subset_open_cover_Icc zero_le_one hc‚ÇÅ hc‚ÇÇ

lemma exists_monotone_Icc_subset_open_cover_unitInterval_prod_self {Œπ} {c : Œπ ‚Üí Set (I √ó I)}
    (hc‚ÇÅ : ‚àÄ i, IsOpen (c i)) (hc‚ÇÇ : univ ‚äÜ ‚ãÉ i, c i) :
    ‚àÉ t : ‚Ñï ‚Üí I, t 0 = 0 ‚àß Monotone t ‚àß (‚àÉ n, ‚àÄ m ‚â• n, t m = 1) ‚àß
      ‚àÄ n m, ‚àÉ i, Icc (t n) (t (n + 1)) √óÀ¢ Icc (t m) (t (m + 1)) ‚äÜ c i := by
  obtain ‚ü®Œ¥, Œ¥_pos, ball_subset‚ü© := lebesgue_number_lemma_of_metric isCompact_univ hc‚ÇÅ hc‚ÇÇ
  have hŒ¥ := half_pos Œ¥_pos
  simp_rw [Subtype.ext_iff]
  have h : (0 : ‚Ñù) ‚â§ 1 := zero_le_one
  refine ‚ü®addNSMul h (Œ¥/2), addNSMul_zero h,
    monotone_addNSMul h hŒ¥.le, addNSMul_eq_right h hŒ¥, fun n m ‚Ü¶ ?_‚ü©
  obtain ‚ü®i, hsub‚ü© := ball_subset (addNSMul h (Œ¥/2) n, addNSMul h (Œ¥/2) m) trivial
  exact ‚ü®i, fun t ht ‚Ü¶ hsub (Metric.mem_ball.mpr <| (max_le (abs_sub_addNSMul_le h hŒ¥.le n ht.1) <|
    abs_sub_addNSMul_le h hŒ¥.le m ht.2).trans_lt <| half_lt_self Œ¥_pos)‚ü©

end partition

@[simp]
theorem projIcc_eq_zero {x : ‚Ñù} : projIcc (0 : ‚Ñù) 1 zero_le_one x = 0 ‚Üî x ‚â§ 0 :=
  projIcc_eq_left zero_lt_one
#align proj_Icc_eq_zero projIcc_eq_zero

@[simp]
theorem projIcc_eq_one {x : ‚Ñù} : projIcc (0 : ‚Ñù) 1 zero_le_one x = 1 ‚Üî 1 ‚â§ x :=
  projIcc_eq_right zero_lt_one
#align proj_Icc_eq_one projIcc_eq_one

namespace Tactic.Interactive

-- Porting note: This replaces an unsafe def tactic
/-- A tactic that solves `0 ‚â§ ‚Üëx`, `0 ‚â§ 1 - ‚Üëx`, `‚Üëx ‚â§ 1`, and `1 - ‚Üëx ‚â§ 1` for `x : I`. -/
macro "unit_interval" : tactic =>
  `(tactic| (first
  | apply unitInterval.nonneg
  | apply unitInterval.one_minus_nonneg
  | apply unitInterval.le_one
  | apply unitInterval.one_minus_le_one))
#noalign tactic.interactive.unit_interval

example (x : unitInterval) : 0 ‚â§ (x : ‚Ñù) := by unit_interval

end Tactic.Interactive

section

variable {ùïú : Type*} [LinearOrderedField ùïú] [TopologicalSpace ùïú] [TopologicalRing ùïú]

-- We only need the ordering on `ùïú` here to avoid talking about flipping the interval over.
-- At the end of the day I only care about `‚Ñù`, so I'm hesitant to put work into generalizing.
/-- The image of `[0,1]` under the homeomorphism `fun x ‚Ü¶ a * x + b` is `[b, a+b]`.
-/
theorem affineHomeomorph_image_I (a b : ùïú) (h : 0 < a) :
    affineHomeomorph a b h.ne.symm '' Set.Icc 0 1 = Set.Icc b (a + b) := by simp [h]
set_option linter.uppercaseLean3 false in
#align affine_homeomorph_image_I affineHomeomorph_image_I

/-- The affine homeomorphism from a nontrivial interval `[a,b]` to `[0,1]`.
-/
def iccHomeoI (a b : ùïú) (h : a < b) : Set.Icc a b ‚âÉ‚Çú Set.Icc (0 : ùïú) (1 : ùïú) := by
  let e := Homeomorph.image (affineHomeomorph (b - a) a (sub_pos.mpr h).ne.symm) (Set.Icc 0 1)
  refine (e.trans ?_).symm
  apply Homeomorph.setCongr
  rw [affineHomeomorph_image_I _ _ (sub_pos.2 h)]
  simp
set_option linter.uppercaseLean3 false in
#align Icc_homeo_I iccHomeoI

@[simp]
theorem iccHomeoI_apply_coe (a b : ùïú) (h : a < b) (x : Set.Icc a b) :
    ((iccHomeoI a b h) x : ùïú) = (x - a) / (b - a) :=
  rfl
set_option linter.uppercaseLean3 false in
#align Icc_homeo_I_apply_coe iccHomeoI_apply_coe

@[simp]
theorem iccHomeoI_symm_apply_coe (a b : ùïú) (h : a < b) (x : Set.Icc (0 : ùïú) (1 : ùïú)) :
    ((iccHomeoI a b h).symm x : ùïú) = (b - a) * x + a :=
  rfl
set_option linter.uppercaseLean3 false in
#align Icc_homeo_I_symm_apply_coe iccHomeoI_symm_apply_coe

end
