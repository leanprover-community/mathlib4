/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Jeremy Avigad
-/
import Mathlib.Order.Filter.Ultrafilter
import Mathlib.Algebra.Support
import Mathlib.Order.Filter.Lift
import Mathlib.Tactic.Continuity

#align_import topology.basic from "leanprover-community/mathlib"@"e354e865255654389cc46e6032160238df2e0f40"

/-!
# Basic theory of topological spaces.

The main definition is the type class `TopologicalSpace Î±` which endows a type `Î±` with a topology.
Then `Set Î±` gets predicates `IsOpen`, `IsClosed` and functions `interior`, `closure` and
`frontier`. Each point `x` of `Î±` gets a neighborhood filter `ğ“ x`. A filter `F` on `Î±` has
`x` as a cluster point if `ClusterPt x F : ğ“ x âŠ“ F â‰  âŠ¥`. A map `f : Î¹ â†’ Î±` clusters at `x`
along `F : Filter Î¹` if `MapClusterPt x F f : ClusterPt x (map f F)`. In particular
the notion of cluster point of a sequence `u` is `MapClusterPt x atTop u`.

For topological spaces `Î±` and `Î²`, a function `f : Î± â†’ Î²` and a point `a : Î±`,
`ContinuousAt f a` means `f` is continuous at `a`, and global continuity is
`Continuous f`. There is also a version of continuity `PContinuous` for
partially defined functions.

## Notation

* `ğ“ x`: the filter `nhds x` of neighborhoods of a point `x`;
* `ğ“Ÿ s`: the principal filter of a set `s`;
* `ğ“[s] x`: the filter `nhdsWithin x s` of neighborhoods of a point `x` within a set `s`;
* `ğ“[â‰¤] x`: the filter `nhdsWithin x (Set.Iic x)` of left-neighborhoods of `x`;
* `ğ“[â‰¥] x`: the filter `nhdsWithin x (Set.Ici x)` of right-neighborhoods of `x`;
* `ğ“[<] x`: the filter `nhdsWithin x (Set.Iio x)` of punctured left-neighborhoods of `x`;
* `ğ“[>] x`: the filter `nhdsWithin x (Set.Ioi x)` of punctured right-neighborhoods of `x`;
* `ğ“[â‰ ] x`: the filter `nhdsWithin x {x}á¶œ` of punctured neighborhoods of `x`.

## Implementation notes

Topology in mathlib heavily uses filters (even more than in Bourbaki). See explanations in
<https://leanprover-community.github.io/theories/topology.html>.

## References

* [N. Bourbaki, *General Topology*][bourbaki1966]
* [I. M. James, *Topologies and Uniformities*][james1999]

## Tags

topological space, interior, closure, frontier, neighborhood, continuity, continuous function
-/

set_option autoImplicit true


noncomputable section

open Set Filter

universe u v w

/-!
### Topological spaces
-/


/-- A topology on `Î±`. -/
class TopologicalSpace (Î± : Type u) where
  /-- A predicate saying that a set is an open set. Use `IsOpen` in the root namespace instead. -/
  protected IsOpen : Set Î± â†’ Prop
  /-- The set representing the whole space is an open set. Use `isOpen_univ` in the root namespace
  instead. -/
  protected isOpen_univ : IsOpen univ
  /-- The intersection of two open sets is an open set. Use `IsOpen.inter` instead. -/
  protected isOpen_inter : âˆ€ s t, IsOpen s â†’ IsOpen t â†’ IsOpen (s âˆ© t)
  /-- The union of a family of open sets is an open set. Use `isOpen_sUnion` in the root namespace
  instead. -/
  protected isOpen_sUnion : âˆ€ s, (âˆ€ t âˆˆ s, IsOpen t) â†’ IsOpen (â‹ƒâ‚€ s)
#align topological_space TopologicalSpace

/-- A constructor for topologies by specifying the closed sets,
and showing that they satisfy the appropriate conditions. -/
def TopologicalSpace.ofClosed {Î± : Type u} (T : Set (Set Î±)) (empty_mem : âˆ… âˆˆ T)
    (sInter_mem : âˆ€ A, A âŠ† T â†’ â‹‚â‚€ A âˆˆ T)
    (union_mem : âˆ€ A, A âˆˆ T â†’ âˆ€ B, B âˆˆ T â†’ A âˆª B âˆˆ T) : TopologicalSpace Î± where
  IsOpen X := Xá¶œ âˆˆ T
  isOpen_univ := by simp [empty_mem]
  isOpen_inter s t hs ht := by simpa only [compl_inter] using union_mem sá¶œ hs tá¶œ ht
  isOpen_sUnion s hs := by
    simp only [Set.compl_sUnion]
    exact sInter_mem (compl '' s) fun z âŸ¨y, hy, hzâŸ© => hz â–¸ hs y hy
#align topological_space.of_closed TopologicalSpace.ofClosed

section TopologicalSpace

variable {Î± : Type u} {Î² : Type v} {Î¹ : Sort w} {a : Î±} {s sâ‚ sâ‚‚ t : Set Î±} {p pâ‚ pâ‚‚ : Î± â†’ Prop}

/-- `IsOpen s` means that `s` is open in the ambient topological space on `Î±` -/
def IsOpen [TopologicalSpace Î±] : Set Î± â†’ Prop := TopologicalSpace.IsOpen
#align is_open IsOpen

set_option quotPrecheck false in
/-- Notation for `IsOpen` with respect to a non-standard topology. -/
scoped[Topology] notation (name := IsOpen_of) "IsOpen[" t "]" => @IsOpen _ t

open Topology

lemma isOpen_mk {p hâ‚ hâ‚‚ hâ‚ƒ} {s : Set Î±} : IsOpen[âŸ¨p, hâ‚, hâ‚‚, hâ‚ƒâŸ©] s â†” p s := Iff.rfl
#align is_open_mk isOpen_mk

@[ext]
protected theorem TopologicalSpace.ext :
    âˆ€ {f g : TopologicalSpace Î±}, IsOpen[f] = IsOpen[g] â†’ f = g
  | âŸ¨_, _, _, _âŸ©, âŸ¨_, _, _, _âŸ©, rfl => rfl
#align topological_space_eq TopologicalSpace.ext

section

variable [TopologicalSpace Î±]

@[simp] theorem isOpen_univ : IsOpen (univ : Set Î±) := TopologicalSpace.isOpen_univ
#align is_open_univ isOpen_univ

theorem IsOpen.inter (hâ‚ : IsOpen sâ‚) (hâ‚‚ : IsOpen sâ‚‚) : IsOpen (sâ‚ âˆ© sâ‚‚) :=
  TopologicalSpace.isOpen_inter sâ‚ sâ‚‚ hâ‚ hâ‚‚
#align is_open.inter IsOpen.inter

theorem isOpen_sUnion {s : Set (Set Î±)} (h : âˆ€ t âˆˆ s, IsOpen t) : IsOpen (â‹ƒâ‚€ s) :=
  TopologicalSpace.isOpen_sUnion s h
#align is_open_sUnion isOpen_sUnion

end

protected theorem TopologicalSpace.ext_iff {t t' : TopologicalSpace Î±} :
    t = t' â†” âˆ€ s, IsOpen[t] s â†” IsOpen[t'] s :=
  âŸ¨fun h s => h â–¸ Iff.rfl, fun h => by ext; exact h _âŸ©
#align topological_space_eq_iff TopologicalSpace.ext_iff

theorem isOpen_fold {s : Set Î±} {t : TopologicalSpace Î±} : t.IsOpen s = IsOpen[t] s :=
  rfl
#align is_open_fold isOpen_fold

variable [TopologicalSpace Î±]

theorem isOpen_iUnion {f : Î¹ â†’ Set Î±} (h : âˆ€ i, IsOpen (f i)) : IsOpen (â‹ƒ i, f i) :=
  isOpen_sUnion (forall_range_iff.2 h)
#align is_open_Union isOpen_iUnion

theorem isOpen_biUnion {s : Set Î²} {f : Î² â†’ Set Î±} (h : âˆ€ i âˆˆ s, IsOpen (f i)) :
    IsOpen (â‹ƒ i âˆˆ s, f i) :=
  isOpen_iUnion fun i => isOpen_iUnion fun hi => h i hi
#align is_open_bUnion isOpen_biUnion

theorem IsOpen.union (hâ‚ : IsOpen sâ‚) (hâ‚‚ : IsOpen sâ‚‚) : IsOpen (sâ‚ âˆª sâ‚‚) := by
  rw [union_eq_iUnion]; exact isOpen_iUnion (Bool.forall_bool.2 âŸ¨hâ‚‚, hâ‚âŸ©)
#align is_open.union IsOpen.union

@[simp] theorem isOpen_empty : IsOpen (âˆ… : Set Î±) := by
  rw [â† sUnion_empty]; exact isOpen_sUnion fun a => False.elim
#align is_open_empty isOpen_empty

theorem isOpen_sInter {s : Set (Set Î±)} (hs : s.Finite) : (âˆ€ t âˆˆ s, IsOpen t) â†’ IsOpen (â‹‚â‚€ s) :=
  Finite.induction_on hs (fun _ => by rw [sInter_empty]; exact isOpen_univ) fun _ _ ih h => by
    simp only [sInter_insert, ball_insert_iff] at h âŠ¢
    exact h.1.inter (ih h.2)
#align is_open_sInter isOpen_sInter

theorem isOpen_biInter {s : Set Î²} {f : Î² â†’ Set Î±} (hs : s.Finite) (h : âˆ€ i âˆˆ s, IsOpen (f i)) :
    IsOpen (â‹‚ i âˆˆ s, f i) :=
  sInter_image f s â–¸ isOpen_sInter (hs.image _) (ball_image_iff.2 h)
#align is_open_bInter isOpen_biInter

theorem isOpen_iInter [Finite Î¹] {s : Î¹ â†’ Set Î±} (h : âˆ€ i, IsOpen (s i)) : IsOpen (â‹‚ i, s i) :=
  isOpen_sInter (finite_range _) (forall_range_iff.2 h)
#align is_open_Inter isOpen_iInter

theorem isOpen_biInter_finset {s : Finset Î²} {f : Î² â†’ Set Î±} (h : âˆ€ i âˆˆ s, IsOpen (f i)) :
    IsOpen (â‹‚ i âˆˆ s, f i) :=
  isOpen_biInter s.finite_toSet h
#align is_open_bInter_finset isOpen_biInter_finset

@[simp] -- porting note: added `simp`
theorem isOpen_const {p : Prop} : IsOpen { _a : Î± | p } := by by_cases p <;> simp [*]
#align is_open_const isOpen_const

theorem IsOpen.and : IsOpen { a | pâ‚ a } â†’ IsOpen { a | pâ‚‚ a } â†’ IsOpen { a | pâ‚ a âˆ§ pâ‚‚ a } :=
  IsOpen.inter
#align is_open.and IsOpen.and

/-- A set is closed if its complement is open -/
class IsClosed (s : Set Î±) : Prop where
  /-- The complement of a closed set is an open set. -/
  isOpen_compl : IsOpen sá¶œ
#align is_closed IsClosed

set_option quotPrecheck false in
/-- Notation for `IsClosed` with respect to a non-standard topology. -/
scoped[Topology] notation (name := IsClosed_of) "IsClosed[" t "]" => @IsClosed _ t

@[simp] theorem isOpen_compl_iff {s : Set Î±} : IsOpen sá¶œ â†” IsClosed s :=
  âŸ¨fun h => âŸ¨hâŸ©, fun h => h.isOpen_complâŸ©
#align is_open_compl_iff isOpen_compl_iff

-- porting note: todo: redefine as `IsClosed s âˆ§ IsOpen s`
/-- A set is clopen if it is both open and closed. -/
def IsClopen (s : Set Î±) : Prop :=
  IsOpen s âˆ§ IsClosed s
#align is_clopen IsClopen

protected theorem IsClopen.isOpen (hs : IsClopen s) : IsOpen s := hs.1
#align is_clopen.is_open IsClopen.isOpen

protected theorem IsClopen.isClosed (hs : IsClopen s) : IsClosed s := hs.2
#align is_clopen.is_closed IsClopen.isClosed

-- porting note: new lemma
theorem isClosed_const {p : Prop} : IsClosed { _a : Î± | p } := âŸ¨isOpen_const (p := Â¬p)âŸ©

@[simp] theorem isClosed_empty : IsClosed (âˆ… : Set Î±) := isClosed_const
#align is_closed_empty isClosed_empty

@[simp] theorem isClosed_univ : IsClosed (univ : Set Î±) := isClosed_const
#align is_closed_univ isClosed_univ

theorem IsClosed.union : IsClosed sâ‚ â†’ IsClosed sâ‚‚ â†’ IsClosed (sâ‚ âˆª sâ‚‚) := by
  simpa only [â† isOpen_compl_iff, compl_union] using IsOpen.inter
#align is_closed.union IsClosed.union

theorem isClosed_sInter {s : Set (Set Î±)} : (âˆ€ t âˆˆ s, IsClosed t) â†’ IsClosed (â‹‚â‚€ s) := by
  simpa only [â† isOpen_compl_iff, compl_sInter, sUnion_image] using isOpen_biUnion
#align is_closed_sInter isClosed_sInter

theorem isClosed_iInter {f : Î¹ â†’ Set Î±} (h : âˆ€ i, IsClosed (f i)) : IsClosed (â‹‚ i, f i) :=
  isClosed_sInter <| forall_range_iff.2 h
#align is_closed_Inter isClosed_iInter

theorem isClosed_biInter {s : Set Î²} {f : Î² â†’ Set Î±} (h : âˆ€ i âˆˆ s, IsClosed (f i)) :
    IsClosed (â‹‚ i âˆˆ s, f i) :=
  isClosed_iInter fun i => isClosed_iInter <| h i
#align is_closed_bInter isClosed_biInter

@[simp]
theorem isClosed_compl_iff {s : Set Î±} : IsClosed sá¶œ â†” IsOpen s := by
  rw [â† isOpen_compl_iff, compl_compl]
#align is_closed_compl_iff isClosed_compl_iff

alias âŸ¨_, IsOpen.isClosed_complâŸ© := isClosed_compl_iff
#align is_open.is_closed_compl IsOpen.isClosed_compl

theorem IsOpen.sdiff {s t : Set Î±} (hâ‚ : IsOpen s) (hâ‚‚ : IsClosed t) : IsOpen (s \ t) :=
  IsOpen.inter hâ‚ hâ‚‚.isOpen_compl
#align is_open.sdiff IsOpen.sdiff

theorem IsClosed.inter (hâ‚ : IsClosed sâ‚) (hâ‚‚ : IsClosed sâ‚‚) : IsClosed (sâ‚ âˆ© sâ‚‚) := by
  rw [â† isOpen_compl_iff] at *
  rw [compl_inter]
  exact IsOpen.union hâ‚ hâ‚‚
#align is_closed.inter IsClosed.inter

theorem IsClosed.sdiff {s t : Set Î±} (hâ‚ : IsClosed s) (hâ‚‚ : IsOpen t) : IsClosed (s \ t) :=
  IsClosed.inter hâ‚ (isClosed_compl_iff.mpr hâ‚‚)
#align is_closed.sdiff IsClosed.sdiff

theorem isClosed_biUnion {s : Set Î²} {f : Î² â†’ Set Î±} (hs : s.Finite) (h : âˆ€ i âˆˆ s, IsClosed (f i)) :
    IsClosed (â‹ƒ i âˆˆ s, f i) := by
  simp only [â† isOpen_compl_iff, compl_iUnion] at *
  exact isOpen_biInter hs h
#align is_closed_bUnion isClosed_biUnion

theorem isClosed_iUnion [Finite Î¹] {s : Î¹ â†’ Set Î±} (h : âˆ€ i, IsClosed (s i)) :
    IsClosed (â‹ƒ i, s i) := by
  simp only [â† isOpen_compl_iff, compl_iUnion] at *
  exact isOpen_iInter h
#align is_closed_Union isClosed_iUnion

theorem isClosed_imp {p q : Î± â†’ Prop} (hp : IsOpen { x | p x }) (hq : IsClosed { x | q x }) :
    IsClosed { x | p x â†’ q x } := by
  simpa only [imp_iff_not_or] using hp.isClosed_compl.union hq
#align is_closed_imp isClosed_imp

theorem IsClosed.not : IsClosed { a | p a } â†’ IsOpen { a | Â¬p a } :=
  isOpen_compl_iff.mpr
#align is_closed.not IsClosed.not

/-!
### Interior of a set
-/

/-- The interior of a set `s` is the largest open subset of `s`. -/
def interior (s : Set Î±) : Set Î± :=
  â‹ƒâ‚€ { t | IsOpen t âˆ§ t âŠ† s }
#align interior interior

-- porting note: use `âˆƒ t, t âŠ† s âˆ§ _` instead of `âˆƒ t âŠ† s, _`
theorem mem_interior {s : Set Î±} {x : Î±} : x âˆˆ interior s â†” âˆƒ t, t âŠ† s âˆ§ IsOpen t âˆ§ x âˆˆ t := by
  simp only [interior, mem_sUnion, mem_setOf_eq, and_assoc, and_left_comm]
#align mem_interior mem_interiorâ‚“

@[simp]
theorem isOpen_interior {s : Set Î±} : IsOpen (interior s) :=
  isOpen_sUnion fun _ => And.left
#align is_open_interior isOpen_interior

theorem interior_subset {s : Set Î±} : interior s âŠ† s :=
  sUnion_subset fun _ => And.right
#align interior_subset interior_subset

theorem interior_maximal {s t : Set Î±} (hâ‚ : t âŠ† s) (hâ‚‚ : IsOpen t) : t âŠ† interior s :=
  subset_sUnion_of_mem âŸ¨hâ‚‚, hâ‚âŸ©
#align interior_maximal interior_maximal

theorem IsOpen.interior_eq {s : Set Î±} (h : IsOpen s) : interior s = s :=
  interior_subset.antisymm (interior_maximal (Subset.refl s) h)
#align is_open.interior_eq IsOpen.interior_eq

theorem interior_eq_iff_isOpen {s : Set Î±} : interior s = s â†” IsOpen s :=
  âŸ¨fun h => h â–¸ isOpen_interior, IsOpen.interior_eqâŸ©
#align interior_eq_iff_is_open interior_eq_iff_isOpen

theorem subset_interior_iff_isOpen {s : Set Î±} : s âŠ† interior s â†” IsOpen s := by
  simp only [interior_eq_iff_isOpen.symm, Subset.antisymm_iff, interior_subset, true_and]
#align subset_interior_iff_is_open subset_interior_iff_isOpen

theorem IsOpen.subset_interior_iff {s t : Set Î±} (hâ‚ : IsOpen s) : s âŠ† interior t â†” s âŠ† t :=
  âŸ¨fun h => Subset.trans h interior_subset, fun hâ‚‚ => interior_maximal hâ‚‚ hâ‚âŸ©
#align is_open.subset_interior_iff IsOpen.subset_interior_iff

theorem subset_interior_iff {s t : Set Î±} : t âŠ† interior s â†” âˆƒ U, IsOpen U âˆ§ t âŠ† U âˆ§ U âŠ† s :=
  âŸ¨fun h => âŸ¨interior s, isOpen_interior, h, interior_subsetâŸ©, fun âŸ¨_U, hU, htU, hUsâŸ© =>
    htU.trans (interior_maximal hUs hU)âŸ©
#align subset_interior_iff subset_interior_iff

@[mono]
theorem interior_mono {s t : Set Î±} (h : s âŠ† t) : interior s âŠ† interior t :=
  interior_maximal (Subset.trans interior_subset h) isOpen_interior
#align interior_mono interior_mono

@[simp]
theorem interior_empty : interior (âˆ… : Set Î±) = âˆ… :=
  isOpen_empty.interior_eq
#align interior_empty interior_empty

@[simp]
theorem interior_univ : interior (univ : Set Î±) = univ :=
  isOpen_univ.interior_eq
#align interior_univ interior_univ

@[simp]
theorem interior_eq_univ {s : Set Î±} : interior s = univ â†” s = univ :=
  âŸ¨fun h => univ_subset_iff.mp <| h.symm.trans_le interior_subset, fun h => h.symm â–¸ interior_univâŸ©
#align interior_eq_univ interior_eq_univ

@[simp]
theorem interior_interior {s : Set Î±} : interior (interior s) = interior s :=
  isOpen_interior.interior_eq
#align interior_interior interior_interior

@[simp]
theorem interior_inter {s t : Set Î±} : interior (s âˆ© t) = interior s âˆ© interior t :=
  Subset.antisymm
    (subset_inter (interior_mono <| inter_subset_left s t)
      (interior_mono <| inter_subset_right s t))
    (interior_maximal (inter_subset_inter interior_subset interior_subset) <|
      IsOpen.inter isOpen_interior isOpen_interior)
#align interior_inter interior_inter

@[simp]
theorem Finset.interior_iInter {Î¹ : Type*} (s : Finset Î¹) (f : Î¹ â†’ Set Î±) :
    interior (â‹‚ i âˆˆ s, f i) = â‹‚ i âˆˆ s, interior (f i) := by
  classical
    refine' s.induction_on (by simp) _
    intro i s _ hâ‚‚
    simp [hâ‚‚]
#align finset.interior_Inter Finset.interior_iInter

-- todo: generalize to `Î¹ : Sort*`
@[simp]
theorem interior_iInter {Î¹ : Type*} [Finite Î¹] (f : Î¹ â†’ Set Î±) :
    interior (â‹‚ i, f i) = â‹‚ i, interior (f i) := by
  cases nonempty_fintype Î¹
  convert Finset.univ.interior_iInter f <;> simp
#align interior_Inter interior_iInter

theorem interior_union_isClosed_of_interior_empty {s t : Set Î±} (hâ‚ : IsClosed s)
    (hâ‚‚ : interior t = âˆ…) : interior (s âˆª t) = interior s :=
  have : interior (s âˆª t) âŠ† s := fun x âŸ¨u, âŸ¨(huâ‚ : IsOpen u), (huâ‚‚ : u âŠ† s âˆª t)âŸ©, (hxâ‚ : x âˆˆ u)âŸ© =>
    by_contradiction fun hxâ‚‚ : x âˆ‰ s =>
      have : u \ s âŠ† t := fun x âŸ¨hâ‚, hâ‚‚âŸ© => Or.resolve_left (huâ‚‚ hâ‚) hâ‚‚
      have : u \ s âŠ† interior t := by rwa [(IsOpen.sdiff huâ‚ hâ‚).subset_interior_iff]
      have : u \ s âŠ† âˆ… := by rwa [hâ‚‚] at this
      this âŸ¨hxâ‚, hxâ‚‚âŸ©
  Subset.antisymm (interior_maximal this isOpen_interior) (interior_mono <| subset_union_left _ _)
#align interior_union_is_closed_of_interior_empty interior_union_isClosed_of_interior_empty

theorem isOpen_iff_forall_mem_open : IsOpen s â†” âˆ€ x âˆˆ s, âˆƒ t, t âŠ† s âˆ§ IsOpen t âˆ§ x âˆˆ t := by
  rw [â† subset_interior_iff_isOpen]
  simp only [subset_def, mem_interior]
#align is_open_iff_forall_mem_open isOpen_iff_forall_mem_open

theorem interior_iInter_subset (s : Î¹ â†’ Set Î±) : interior (â‹‚ i, s i) âŠ† â‹‚ i, interior (s i) :=
  subset_iInter fun _ => interior_mono <| iInter_subset _ _
#align interior_Inter_subset interior_iInter_subset

theorem interior_Interâ‚‚_subset (p : Î¹ â†’ Sort*) (s : âˆ€ i, p i â†’ Set Î±) :
    interior (â‹‚ (i) (j), s i j) âŠ† â‹‚ (i) (j), interior (s i j) :=
  (interior_iInter_subset _).trans <| iInter_mono fun _ => interior_iInter_subset _
#align interior_Interâ‚‚_subset interior_Interâ‚‚_subset

theorem interior_sInter_subset (S : Set (Set Î±)) : interior (â‹‚â‚€ S) âŠ† â‹‚ s âˆˆ S, interior s :=
  calc
    interior (â‹‚â‚€ S) = interior (â‹‚ s âˆˆ S, s) := by rw [sInter_eq_biInter]
    _ âŠ† â‹‚ s âˆˆ S, interior s := interior_Interâ‚‚_subset _ _
#align interior_sInter_subset interior_sInter_subset

/-!
### Closure of a set
-/


/-- The closure of `s` is the smallest closed set containing `s`. -/
def closure (s : Set Î±) : Set Î± :=
  â‹‚â‚€ { t | IsClosed t âˆ§ s âŠ† t }
#align closure closure

set_option quotPrecheck false in
/-- Notation for `closure` with respect to a non-standard topology. -/
scoped[Topology] notation (name := closure_of) "closure[" t "]" => @closure _ t

@[simp]
theorem isClosed_closure {s : Set Î±} : IsClosed (closure s) :=
  isClosed_sInter fun _ => And.left
#align is_closed_closure isClosed_closure

theorem subset_closure {s : Set Î±} : s âŠ† closure s :=
  subset_sInter fun _ => And.right
#align subset_closure subset_closure

theorem not_mem_of_not_mem_closure {s : Set Î±} {P : Î±} (hP : P âˆ‰ closure s) : P âˆ‰ s := fun h =>
  hP (subset_closure h)
#align not_mem_of_not_mem_closure not_mem_of_not_mem_closure

theorem closure_minimal {s t : Set Î±} (hâ‚ : s âŠ† t) (hâ‚‚ : IsClosed t) : closure s âŠ† t :=
  sInter_subset_of_mem âŸ¨hâ‚‚, hâ‚âŸ©
#align closure_minimal closure_minimal

theorem Disjoint.closure_left {s t : Set Î±} (hd : Disjoint s t) (ht : IsOpen t) :
    Disjoint (closure s) t :=
  disjoint_compl_left.mono_left <| closure_minimal hd.subset_compl_right ht.isClosed_compl
#align disjoint.closure_left Disjoint.closure_left

theorem Disjoint.closure_right {s t : Set Î±} (hd : Disjoint s t) (hs : IsOpen s) :
    Disjoint s (closure t) :=
  (hd.symm.closure_left hs).symm
#align disjoint.closure_right Disjoint.closure_right

theorem IsClosed.closure_eq {s : Set Î±} (h : IsClosed s) : closure s = s :=
  Subset.antisymm (closure_minimal (Subset.refl s) h) subset_closure
#align is_closed.closure_eq IsClosed.closure_eq

theorem IsClosed.closure_subset {s : Set Î±} (hs : IsClosed s) : closure s âŠ† s :=
  closure_minimal (Subset.refl _) hs
#align is_closed.closure_subset IsClosed.closure_subset

theorem IsClosed.closure_subset_iff {s t : Set Î±} (hâ‚ : IsClosed t) : closure s âŠ† t â†” s âŠ† t :=
  âŸ¨Subset.trans subset_closure, fun h => closure_minimal h hâ‚âŸ©
#align is_closed.closure_subset_iff IsClosed.closure_subset_iff

theorem IsClosed.mem_iff_closure_subset {s : Set Î±} (hs : IsClosed s) {x : Î±} :
    x âˆˆ s â†” closure ({x} : Set Î±) âŠ† s :=
  (hs.closure_subset_iff.trans Set.singleton_subset_iff).symm
#align is_closed.mem_iff_closure_subset IsClosed.mem_iff_closure_subset

@[mono]
theorem closure_mono {s t : Set Î±} (h : s âŠ† t) : closure s âŠ† closure t :=
  closure_minimal (Subset.trans h subset_closure) isClosed_closure
#align closure_mono closure_mono

theorem monotone_closure (Î± : Type*) [TopologicalSpace Î±] : Monotone (@closure Î± _) := fun _ _ =>
  closure_mono
#align monotone_closure monotone_closure

theorem diff_subset_closure_iff {s t : Set Î±} : s \ t âŠ† closure t â†” s âŠ† closure t := by
  rw [diff_subset_iff, union_eq_self_of_subset_left subset_closure]
#align diff_subset_closure_iff diff_subset_closure_iff

theorem closure_inter_subset_inter_closure (s t : Set Î±) :
    closure (s âˆ© t) âŠ† closure s âˆ© closure t :=
  (monotone_closure Î±).map_inf_le s t
#align closure_inter_subset_inter_closure closure_inter_subset_inter_closure

theorem isClosed_of_closure_subset {s : Set Î±} (h : closure s âŠ† s) : IsClosed s := by
  rw [subset_closure.antisymm h]; exact isClosed_closure
#align is_closed_of_closure_subset isClosed_of_closure_subset

theorem closure_eq_iff_isClosed {s : Set Î±} : closure s = s â†” IsClosed s :=
  âŸ¨fun h => h â–¸ isClosed_closure, IsClosed.closure_eqâŸ©
#align closure_eq_iff_is_closed closure_eq_iff_isClosed

theorem closure_subset_iff_isClosed {s : Set Î±} : closure s âŠ† s â†” IsClosed s :=
  âŸ¨isClosed_of_closure_subset, IsClosed.closure_subsetâŸ©
#align closure_subset_iff_is_closed closure_subset_iff_isClosed

@[simp]
theorem closure_empty : closure (âˆ… : Set Î±) = âˆ… :=
  isClosed_empty.closure_eq
#align closure_empty closure_empty

@[simp]
theorem closure_empty_iff (s : Set Î±) : closure s = âˆ… â†” s = âˆ… :=
  âŸ¨subset_eq_empty subset_closure, fun h => h.symm â–¸ closure_emptyâŸ©
#align closure_empty_iff closure_empty_iff

@[simp]
theorem closure_nonempty_iff {s : Set Î±} : (closure s).Nonempty â†” s.Nonempty := by
  simp only [nonempty_iff_ne_empty, Ne.def, closure_empty_iff]
#align closure_nonempty_iff closure_nonempty_iff

alias âŸ¨Set.Nonempty.of_closure, Set.Nonempty.closureâŸ© := closure_nonempty_iff
#align set.nonempty.of_closure Set.Nonempty.of_closure
#align set.nonempty.closure Set.Nonempty.closure

@[simp]
theorem closure_univ : closure (univ : Set Î±) = univ :=
  isClosed_univ.closure_eq
#align closure_univ closure_univ

@[simp]
theorem closure_closure {s : Set Î±} : closure (closure s) = closure s :=
  isClosed_closure.closure_eq
#align closure_closure closure_closure

@[simp]
theorem closure_union {s t : Set Î±} : closure (s âˆª t) = closure s âˆª closure t :=
  Subset.antisymm
    (closure_minimal (union_subset_union subset_closure subset_closure) <|
      IsClosed.union isClosed_closure isClosed_closure)
    ((monotone_closure Î±).le_map_sup s t)
#align closure_union closure_union

@[simp]
theorem Finset.closure_biUnion {Î¹ : Type*} (s : Finset Î¹) (f : Î¹ â†’ Set Î±) :
    closure (â‹ƒ i âˆˆ s, f i) = â‹ƒ i âˆˆ s, closure (f i) := by
  classical
    refine' s.induction_on (by simp) _
    intro i s _ hâ‚‚
    simp [hâ‚‚]
#align finset.closure_bUnion Finset.closure_biUnion

@[simp]
theorem closure_iUnion {Î¹ : Type*} [Finite Î¹] (f : Î¹ â†’ Set Î±) :
    closure (â‹ƒ i, f i) = â‹ƒ i, closure (f i) := by
  cases nonempty_fintype Î¹
  convert Finset.univ.closure_biUnion f <;> simp
#align closure_Union closure_iUnion

theorem interior_subset_closure {s : Set Î±} : interior s âŠ† closure s :=
  Subset.trans interior_subset subset_closure
#align interior_subset_closure interior_subset_closure

theorem closure_eq_compl_interior_compl {s : Set Î±} : closure s = (interior sá¶œ)á¶œ := by
  rw [interior, closure, compl_sUnion, compl_image_set_of]
  simp only [compl_subset_compl, isOpen_compl_iff]
#align closure_eq_compl_interior_compl closure_eq_compl_interior_compl

@[simp]
theorem interior_compl {s : Set Î±} : interior sá¶œ = (closure s)á¶œ := by
  simp [closure_eq_compl_interior_compl]
#align interior_compl interior_compl

@[simp]
theorem closure_compl {s : Set Î±} : closure sá¶œ = (interior s)á¶œ := by
  simp [closure_eq_compl_interior_compl]
#align closure_compl closure_compl

theorem mem_closure_iff {s : Set Î±} {a : Î±} :
    a âˆˆ closure s â†” âˆ€ o, IsOpen o â†’ a âˆˆ o â†’ (o âˆ© s).Nonempty :=
  âŸ¨fun h o oo ao =>
    by_contradiction fun os =>
      have : s âŠ† oá¶œ := fun x xs xo => os âŸ¨x, xo, xsâŸ©
      closure_minimal this (isClosed_compl_iff.2 oo) h ao,
    fun H _ âŸ¨hâ‚, hâ‚‚âŸ© =>
    by_contradiction fun nc =>
      let âŸ¨_, hc, hsâŸ© := H _ hâ‚.isOpen_compl nc
      hc (hâ‚‚ hs)âŸ©
#align mem_closure_iff mem_closure_iff

theorem closure_inter_open_nonempty_iff {s t : Set Î±} (h : IsOpen t) :
    (closure s âˆ© t).Nonempty â†” (s âˆ© t).Nonempty :=
  âŸ¨fun âŸ¨_x, hxcs, hxtâŸ© => inter_comm t s â–¸ mem_closure_iff.1 hxcs t h hxt, fun h =>
    h.mono <| inf_le_inf_right t subset_closureâŸ©
#align closure_inter_open_nonempty_iff closure_inter_open_nonempty_iff

theorem Filter.le_lift'_closure (l : Filter Î±) : l â‰¤ l.lift' closure :=
  le_lift'.2 fun _ h => mem_of_superset h subset_closure
#align filter.le_lift'_closure Filter.le_lift'_closure

theorem Filter.HasBasis.lift'_closure {l : Filter Î±} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set Î±}
    (h : l.HasBasis p s) : (l.lift' closure).HasBasis p fun i => closure (s i) :=
  h.lift' (monotone_closure Î±)
#align filter.has_basis.lift'_closure Filter.HasBasis.lift'_closure

theorem Filter.HasBasis.lift'_closure_eq_self {l : Filter Î±} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set Î±}
    (h : l.HasBasis p s) (hc : âˆ€ i, p i â†’ IsClosed (s i)) : l.lift' closure = l :=
  le_antisymm (h.ge_iff.2 fun i hi => (hc i hi).closure_eq â–¸ mem_lift' (h.mem_of_mem hi))
    l.le_lift'_closure
#align filter.has_basis.lift'_closure_eq_self Filter.HasBasis.lift'_closure_eq_self

@[simp]
theorem Filter.lift'_closure_eq_bot {l : Filter Î±} : l.lift' closure = âŠ¥ â†” l = âŠ¥ :=
  âŸ¨fun h => bot_unique <| h â–¸ l.le_lift'_closure, fun h =>
    h.symm â–¸ by rw [lift'_bot (monotone_closure _), closure_empty, principal_empty]âŸ©
#align filter.lift'_closure_eq_bot Filter.lift'_closure_eq_bot

/-- A set is dense in a topological space if every point belongs to its closure. -/
def Dense (s : Set Î±) : Prop :=
  âˆ€ x, x âˆˆ closure s
#align dense Dense

theorem dense_iff_closure_eq {s : Set Î±} : Dense s â†” closure s = univ :=
  eq_univ_iff_forall.symm
#align dense_iff_closure_eq dense_iff_closure_eq

alias âŸ¨Dense.closure_eq, _âŸ© := dense_iff_closure_eq
#align dense.closure_eq Dense.closure_eq

theorem interior_eq_empty_iff_dense_compl {s : Set Î±} : interior s = âˆ… â†” Dense sá¶œ := by
  rw [dense_iff_closure_eq, closure_compl, compl_univ_iff]
#align interior_eq_empty_iff_dense_compl interior_eq_empty_iff_dense_compl

theorem Dense.interior_compl {s : Set Î±} (h : Dense s) : interior sá¶œ = âˆ… :=
  interior_eq_empty_iff_dense_compl.2 <| by rwa [compl_compl]
#align dense.interior_compl Dense.interior_compl

/-- The closure of a set `s` is dense if and only if `s` is dense. -/
@[simp]
theorem dense_closure {s : Set Î±} : Dense (closure s) â†” Dense s := by
  rw [Dense, Dense, closure_closure]
#align dense_closure dense_closure

protected alias âŸ¨_, Dense.closureâŸ© := dense_closure
alias âŸ¨Dense.of_closure, _âŸ© := dense_closure
#align dense.of_closure Dense.of_closure
#align dense.closure Dense.closure

@[simp]
theorem dense_univ : Dense (univ : Set Î±) := fun _ => subset_closure trivial
#align dense_univ dense_univ

/-- A set is dense if and only if it has a nonempty intersection with each nonempty open set. -/
theorem dense_iff_inter_open {s : Set Î±} :
    Dense s â†” âˆ€ U, IsOpen U â†’ U.Nonempty â†’ (U âˆ© s).Nonempty := by
  constructor <;> intro h
  Â· rintro U U_op âŸ¨x, x_inâŸ©
    exact mem_closure_iff.1 (h _) U U_op x_in
  Â· intro x
    rw [mem_closure_iff]
    intro U U_op x_in
    exact h U U_op âŸ¨_, x_inâŸ©
#align dense_iff_inter_open dense_iff_inter_open

alias âŸ¨Dense.inter_open_nonempty, _âŸ© := dense_iff_inter_open
#align dense.inter_open_nonempty Dense.inter_open_nonempty

theorem Dense.exists_mem_open {s : Set Î±} (hs : Dense s) {U : Set Î±} (ho : IsOpen U)
    (hne : U.Nonempty) : âˆƒ x âˆˆ s, x âˆˆ U :=
  let âŸ¨x, hxâŸ© := hs.inter_open_nonempty U ho hne
  âŸ¨x, hx.2, hx.1âŸ©
#align dense.exists_mem_open Dense.exists_mem_open

theorem Dense.nonempty_iff {s : Set Î±} (hs : Dense s) : s.Nonempty â†” Nonempty Î± :=
  âŸ¨fun âŸ¨x, _âŸ© => âŸ¨xâŸ©, fun âŸ¨xâŸ© =>
    let âŸ¨y, hyâŸ© := hs.inter_open_nonempty _ isOpen_univ âŸ¨x, trivialâŸ©
    âŸ¨y, hy.2âŸ©âŸ©
#align dense.nonempty_iff Dense.nonempty_iff

theorem Dense.nonempty [h : Nonempty Î±] {s : Set Î±} (hs : Dense s) : s.Nonempty :=
  hs.nonempty_iff.2 h
#align dense.nonempty Dense.nonempty

@[mono]
theorem Dense.mono {sâ‚ sâ‚‚ : Set Î±} (h : sâ‚ âŠ† sâ‚‚) (hd : Dense sâ‚) : Dense sâ‚‚ := fun x =>
  closure_mono h (hd x)
#align dense.mono Dense.mono

/-- Complement to a singleton is dense if and only if the singleton is not an open set. -/
theorem dense_compl_singleton_iff_not_open {x : Î±} :
    Dense ({x}á¶œ : Set Î±) â†” Â¬IsOpen ({x} : Set Î±) := by
  constructor
  Â· intro hd ho
    exact (hd.inter_open_nonempty _ ho (singleton_nonempty _)).ne_empty (inter_compl_self _)
  Â· refine' fun ho => dense_iff_inter_open.2 fun U hU hne => inter_compl_nonempty_iff.2 fun hUx => _
    obtain rfl : U = {x}
    exact eq_singleton_iff_nonempty_unique_mem.2 âŸ¨hne, hUxâŸ©
    exact ho hU
#align dense_compl_singleton_iff_not_open dense_compl_singleton_iff_not_open

/-!
### Frontier of a set
-/

/-- The frontier of a set is the set of points between the closure and interior. -/
def frontier (s : Set Î±) : Set Î± :=
  closure s \ interior s
#align frontier frontier

@[simp]
theorem closure_diff_interior (s : Set Î±) : closure s \ interior s = frontier s :=
  rfl
#align closure_diff_interior closure_diff_interior

@[simp]
theorem closure_diff_frontier (s : Set Î±) : closure s \ frontier s = interior s := by
  rw [frontier, diff_diff_right_self, inter_eq_self_of_subset_right interior_subset_closure]
#align closure_diff_frontier closure_diff_frontier

@[simp]
theorem self_diff_frontier (s : Set Î±) : s \ frontier s = interior s := by
  rw [frontier, diff_diff_right, diff_eq_empty.2 subset_closure,
    inter_eq_self_of_subset_right interior_subset, empty_union]
#align self_diff_frontier self_diff_frontier

theorem frontier_eq_closure_inter_closure {s : Set Î±} : frontier s = closure s âˆ© closure sá¶œ := by
  rw [closure_compl, frontier, diff_eq]
#align frontier_eq_closure_inter_closure frontier_eq_closure_inter_closure

theorem frontier_subset_closure {s : Set Î±} : frontier s âŠ† closure s :=
  diff_subset _ _
#align frontier_subset_closure frontier_subset_closure

theorem IsClosed.frontier_subset (hs : IsClosed s) : frontier s âŠ† s :=
  frontier_subset_closure.trans hs.closure_eq.subset
#align is_closed.frontier_subset IsClosed.frontier_subset

theorem frontier_closure_subset {s : Set Î±} : frontier (closure s) âŠ† frontier s :=
  diff_subset_diff closure_closure.subset <| interior_mono subset_closure
#align frontier_closure_subset frontier_closure_subset

theorem frontier_interior_subset {s : Set Î±} : frontier (interior s) âŠ† frontier s :=
  diff_subset_diff (closure_mono interior_subset) interior_interior.symm.subset
#align frontier_interior_subset frontier_interior_subset

/-- The complement of a set has the same frontier as the original set. -/
@[simp]
theorem frontier_compl (s : Set Î±) : frontier sá¶œ = frontier s := by
  simp only [frontier_eq_closure_inter_closure, compl_compl, inter_comm]
#align frontier_compl frontier_compl

@[simp]
theorem frontier_univ : frontier (univ : Set Î±) = âˆ… := by simp [frontier]
#align frontier_univ frontier_univ

@[simp]
theorem frontier_empty : frontier (âˆ… : Set Î±) = âˆ… := by simp [frontier]
#align frontier_empty frontier_empty

theorem frontier_inter_subset (s t : Set Î±) :
    frontier (s âˆ© t) âŠ† frontier s âˆ© closure t âˆª closure s âˆ© frontier t := by
  simp only [frontier_eq_closure_inter_closure, compl_inter, closure_union]
  refine' (inter_subset_inter_left _ (closure_inter_subset_inter_closure s t)).trans_eq _
  simp only [inter_distrib_left, inter_distrib_right, inter_assoc, inter_comm (closure t)]
#align frontier_inter_subset frontier_inter_subset

theorem frontier_union_subset (s t : Set Î±) :
    frontier (s âˆª t) âŠ† frontier s âˆ© closure tá¶œ âˆª closure sá¶œ âˆ© frontier t := by
  simpa only [frontier_compl, â† compl_union] using frontier_inter_subset sá¶œ tá¶œ
#align frontier_union_subset frontier_union_subset

theorem IsClosed.frontier_eq {s : Set Î±} (hs : IsClosed s) : frontier s = s \ interior s := by
  rw [frontier, hs.closure_eq]
#align is_closed.frontier_eq IsClosed.frontier_eq

theorem IsOpen.frontier_eq {s : Set Î±} (hs : IsOpen s) : frontier s = closure s \ s := by
  rw [frontier, hs.interior_eq]
#align is_open.frontier_eq IsOpen.frontier_eq

theorem IsOpen.inter_frontier_eq {s : Set Î±} (hs : IsOpen s) : s âˆ© frontier s = âˆ… := by
  rw [hs.frontier_eq, inter_diff_self]
#align is_open.inter_frontier_eq IsOpen.inter_frontier_eq

/-- The frontier of a set is closed. -/
theorem isClosed_frontier {s : Set Î±} : IsClosed (frontier s) := by
  rw [frontier_eq_closure_inter_closure]; exact IsClosed.inter isClosed_closure isClosed_closure
#align is_closed_frontier isClosed_frontier

/-- The frontier of a closed set has no interior point. -/
theorem interior_frontier {s : Set Î±} (h : IsClosed s) : interior (frontier s) = âˆ… := by
  have A : frontier s = s \ interior s := h.frontier_eq
  have B : interior (frontier s) âŠ† interior s := by rw [A]; exact interior_mono (diff_subset _ _)
  have C : interior (frontier s) âŠ† frontier s := interior_subset
  have : interior (frontier s) âŠ† interior s âˆ© (s \ interior s) :=
    subset_inter B (by simpa [A] using C)
  rwa [inter_diff_self, subset_empty_iff] at this
#align interior_frontier interior_frontier

theorem closure_eq_interior_union_frontier (s : Set Î±) : closure s = interior s âˆª frontier s :=
  (union_diff_cancel interior_subset_closure).symm
#align closure_eq_interior_union_frontier closure_eq_interior_union_frontier

theorem closure_eq_self_union_frontier (s : Set Î±) : closure s = s âˆª frontier s :=
  (union_diff_cancel' interior_subset subset_closure).symm
#align closure_eq_self_union_frontier closure_eq_self_union_frontier

theorem Disjoint.frontier_left (ht : IsOpen t) (hd : Disjoint s t) : Disjoint (frontier s) t :=
  subset_compl_iff_disjoint_right.1 <|
    frontier_subset_closure.trans <| closure_minimal (disjoint_left.1 hd) <| isClosed_compl_iff.2 ht
#align disjoint.frontier_left Disjoint.frontier_left

theorem Disjoint.frontier_right (hs : IsOpen s) (hd : Disjoint s t) : Disjoint s (frontier t) :=
  (hd.symm.frontier_left hs).symm
#align disjoint.frontier_right Disjoint.frontier_right

theorem frontier_eq_inter_compl_interior {s : Set Î±} :
    frontier s = (interior s)á¶œ âˆ© (interior sá¶œ)á¶œ := by
  rw [â† frontier_compl, â† closure_compl]; rfl
#align frontier_eq_inter_compl_interior frontier_eq_inter_compl_interior

theorem compl_frontier_eq_union_interior {s : Set Î±} :
    (frontier s)á¶œ = interior s âˆª interior sá¶œ := by
  rw [frontier_eq_inter_compl_interior]
  simp only [compl_inter, compl_compl]
#align compl_frontier_eq_union_interior compl_frontier_eq_union_interior

/-!
### Neighborhoods
-/

/-- A set is called a neighborhood of `a` if it contains an open set around `a`. The set of all
neighborhoods of `a` forms a filter, the neighborhood filter at `a`, is here defined as the
infimum over the principal filters of all open sets containing `a`. -/
irreducible_def nhds (a : Î±) : Filter Î± :=
  â¨… s âˆˆ { s : Set Î± | a âˆˆ s âˆ§ IsOpen s }, ğ“Ÿ s
#align nhds nhds
#align nhds_def nhds_def

/-- The "neighborhood within" filter. Elements of `ğ“[s] a` are sets containing the
intersection of `s` and a neighborhood of `a`. -/
def nhdsWithin (a : Î±) (s : Set Î±) : Filter Î± :=
  nhds a âŠ“ ğ“Ÿ s
#align nhds_within nhdsWithin

section

@[inherit_doc]
scoped[Topology] notation "ğ“" => nhds

@[inherit_doc]
scoped[Topology] notation "ğ“[" s "] " x:100 => nhdsWithin x s

/-- Notation for the filter of punctured neighborhoods of a point. -/
scoped[Topology] notation "ğ“[â‰ ] " x:100 => nhdsWithin x {x}á¶œ

/-- Notation for the filter of right neighborhoods of a point. -/
scoped[Topology] notation "ğ“[â‰¥] " x:100 => nhdsWithin x (Set.Ici x)

/-- Notation for the filter of left neighborhoods of a point. -/
scoped[Topology] notation "ğ“[â‰¤] " x:100 => nhdsWithin x (Set.Iic x)

/-- Notation for the filter of punctured right neighborhoods of a point. -/
scoped[Topology] notation "ğ“[>] " x:100 => nhdsWithin x (Set.Ioi x)

/-- Notation for the filter of punctured left neighborhoods of a point. -/
scoped[Topology] notation "ğ“[<] " x:100 => nhdsWithin x (Set.Iio x)

end

theorem nhds_def' (a : Î±) : ğ“ a = â¨… (s : Set Î±) (_ : IsOpen s) (_ : a âˆˆ s), ğ“Ÿ s := by
  simp only [nhds_def, mem_setOf_eq, @and_comm (a âˆˆ _), iInf_and]
#align nhds_def' nhds_def'

/-- The open sets containing `a` are a basis for the neighborhood filter. See `nhds_basis_opens'`
for a variant using open neighborhoods instead. -/
theorem nhds_basis_opens (a : Î±) :
    (ğ“ a).HasBasis (fun s : Set Î± => a âˆˆ s âˆ§ IsOpen s) fun s => s := by
  rw [nhds_def]
  exact hasBasis_biInf_principal
    (fun s âŸ¨has, hsâŸ© t âŸ¨hat, htâŸ© =>
      âŸ¨s âˆ© t, âŸ¨âŸ¨has, hatâŸ©, IsOpen.inter hs htâŸ©, âŸ¨inter_subset_left _ _, inter_subset_right _ _âŸ©âŸ©)
    âŸ¨univ, âŸ¨mem_univ a, isOpen_univâŸ©âŸ©
#align nhds_basis_opens nhds_basis_opens

theorem nhds_basis_closeds (a : Î±) : (ğ“ a).HasBasis (fun s : Set Î± => a âˆ‰ s âˆ§ IsClosed s) compl :=
  âŸ¨fun t => (nhds_basis_opens a).mem_iff.trans <|
    compl_surjective.exists.trans <| by simp only [isOpen_compl_iff, mem_compl_iff]âŸ©
#align nhds_basis_closeds nhds_basis_closeds

/-- A filter lies below the neighborhood filter at `a` iff it contains every open set around `a`. -/
theorem le_nhds_iff {f a} : f â‰¤ ğ“ a â†” âˆ€ s : Set Î±, a âˆˆ s â†’ IsOpen s â†’ s âˆˆ f := by simp [nhds_def]
#align le_nhds_iff le_nhds_iff

/-- To show a filter is above the neighborhood filter at `a`, it suffices to show that it is above
the principal filter of some open set `s` containing `a`. -/
theorem nhds_le_of_le {f a} {s : Set Î±} (h : a âˆˆ s) (o : IsOpen s) (sf : ğ“Ÿ s â‰¤ f) : ğ“ a â‰¤ f := by
  rw [nhds_def]; exact iInfâ‚‚_le_of_le s âŸ¨h, oâŸ© sf
#align nhds_le_of_le nhds_le_of_le

-- porting note: use `âˆƒ t, t âŠ† s âˆ§ _` instead of `âˆƒ t âŠ† s, _`
theorem mem_nhds_iff {a : Î±} {s : Set Î±} : s âˆˆ ğ“ a â†” âˆƒ t, t âŠ† s âˆ§ IsOpen t âˆ§ a âˆˆ t :=
  (nhds_basis_opens a).mem_iff.trans <| exists_congr <| fun _ =>
    âŸ¨fun h => âŸ¨h.2, h.1.2, h.1.1âŸ©, fun h => âŸ¨âŸ¨h.2.2, h.2.1âŸ©, h.1âŸ©âŸ©
#align mem_nhds_iff mem_nhds_iffâ‚“

/-- A predicate is true in a neighborhood of `a` iff it is true for all the points in an open set
containing `a`. -/
theorem eventually_nhds_iff {a : Î±} {p : Î± â†’ Prop} :
    (âˆ€á¶  x in ğ“ a, p x) â†” âˆƒ t : Set Î±, (âˆ€ x âˆˆ t, p x) âˆ§ IsOpen t âˆ§ a âˆˆ t :=
  mem_nhds_iff.trans <| by simp only [subset_def, exists_prop, mem_setOf_eq]
#align eventually_nhds_iff eventually_nhds_iff

theorem mem_interior_iff_mem_nhds {s : Set Î±} {a : Î±} : a âˆˆ interior s â†” s âˆˆ ğ“ a :=
  mem_interior.trans mem_nhds_iff.symm
#align mem_interior_iff_mem_nhds mem_interior_iff_mem_nhds

theorem map_nhds {a : Î±} {f : Î± â†’ Î²} :
    map f (ğ“ a) = â¨… s âˆˆ { s : Set Î± | a âˆˆ s âˆ§ IsOpen s }, ğ“Ÿ (image f s) :=
  ((nhds_basis_opens a).map f).eq_biInf
#align map_nhds map_nhds

theorem mem_of_mem_nhds {a : Î±} {s : Set Î±} : s âˆˆ ğ“ a â†’ a âˆˆ s := fun H =>
  let âŸ¨_t, ht, _, hsâŸ© := mem_nhds_iff.1 H; ht hs
#align mem_of_mem_nhds mem_of_mem_nhds

/-- If a predicate is true in a neighborhood of `a`, then it is true for `a`. -/
theorem Filter.Eventually.self_of_nhds {p : Î± â†’ Prop} {a : Î±} (h : âˆ€á¶  y in ğ“ a, p y) : p a :=
  mem_of_mem_nhds h
#align filter.eventually.self_of_nhds Filter.Eventually.self_of_nhds

theorem IsOpen.mem_nhds {a : Î±} {s : Set Î±} (hs : IsOpen s) (ha : a âˆˆ s) : s âˆˆ ğ“ a :=
  mem_nhds_iff.2 âŸ¨s, Subset.refl _, hs, haâŸ©
#align is_open.mem_nhds IsOpen.mem_nhds

protected theorem IsOpen.mem_nhds_iff {a : Î±} {s : Set Î±} (hs : IsOpen s) : s âˆˆ ğ“ a â†” a âˆˆ s :=
  âŸ¨mem_of_mem_nhds, fun ha => mem_nhds_iff.2 âŸ¨s, Subset.rfl, hs, haâŸ©âŸ©
#align is_open.mem_nhds_iff IsOpen.mem_nhds_iff

theorem IsClosed.compl_mem_nhds {a : Î±} {s : Set Î±} (hs : IsClosed s) (ha : a âˆ‰ s) : sá¶œ âˆˆ ğ“ a :=
  hs.isOpen_compl.mem_nhds (mem_compl ha)
#align is_closed.compl_mem_nhds IsClosed.compl_mem_nhds

theorem IsOpen.eventually_mem {a : Î±} {s : Set Î±} (hs : IsOpen s) (ha : a âˆˆ s) :
    âˆ€á¶  x in ğ“ a, x âˆˆ s :=
  IsOpen.mem_nhds hs ha
#align is_open.eventually_mem IsOpen.eventually_mem

/-- The open neighborhoods of `a` are a basis for the neighborhood filter. See `nhds_basis_opens`
for a variant using open sets around `a` instead. -/
theorem nhds_basis_opens' (a : Î±) :
    (ğ“ a).HasBasis (fun s : Set Î± => s âˆˆ ğ“ a âˆ§ IsOpen s) fun x => x := by
  convert nhds_basis_opens a using 2
  exact and_congr_left_iff.2 IsOpen.mem_nhds_iff
#align nhds_basis_opens' nhds_basis_opens'

/-- If `U` is a neighborhood of each point of a set `s` then it is a neighborhood of `s`:
it contains an open set containing `s`. -/
theorem exists_open_set_nhds {s U : Set Î±} (h : âˆ€ x âˆˆ s, U âˆˆ ğ“ x) :
    âˆƒ V : Set Î±, s âŠ† V âˆ§ IsOpen V âˆ§ V âŠ† U :=
  âŸ¨interior U, fun x hx => mem_interior_iff_mem_nhds.2 <| h x hx, isOpen_interior, interior_subsetâŸ©
#align exists_open_set_nhds exists_open_set_nhds

/-- If `U` is a neighborhood of each point of a set `s` then it is a neighborhood of s:
it contains an open set containing `s`. -/
theorem exists_open_set_nhds' {s U : Set Î±} (h : U âˆˆ â¨† x âˆˆ s, ğ“ x) :
    âˆƒ V : Set Î±, s âŠ† V âˆ§ IsOpen V âˆ§ V âŠ† U :=
  exists_open_set_nhds (by simpa using h)
#align exists_open_set_nhds' exists_open_set_nhds'

/-- If a predicate is true in a neighbourhood of `a`, then for `y` sufficiently close
to `a` this predicate is true in a neighbourhood of `y`. -/
theorem Filter.Eventually.eventually_nhds {p : Î± â†’ Prop} {a : Î±} (h : âˆ€á¶  y in ğ“ a, p y) :
    âˆ€á¶  y in ğ“ a, âˆ€á¶  x in ğ“ y, p x :=
  let âŸ¨t, htp, hto, haâŸ© := eventually_nhds_iff.1 h
  eventually_nhds_iff.2 âŸ¨t, fun _x hx => eventually_nhds_iff.2 âŸ¨t, htp, hto, hxâŸ©, hto, haâŸ©
#align filter.eventually.eventually_nhds Filter.Eventually.eventually_nhds

@[simp]
theorem eventually_eventually_nhds {p : Î± â†’ Prop} {a : Î±} :
    (âˆ€á¶  y in ğ“ a, âˆ€á¶  x in ğ“ y, p x) â†” âˆ€á¶  x in ğ“ a, p x :=
  âŸ¨fun h => h.self_of_nhds, fun h => h.eventually_nhdsâŸ©
#align eventually_eventually_nhds eventually_eventually_nhds

@[simp]
theorem frequently_frequently_nhds {p : Î± â†’ Prop} {a : Î±} :
    (âˆƒá¶  y in ğ“ a, âˆƒá¶  x in ğ“ y, p x) â†” âˆƒá¶  x in ğ“ a, p x := by
  rw [â† not_iff_not]
  simp only [not_frequently, eventually_eventually_nhds]
#align frequently_frequently_nhds frequently_frequently_nhds

@[simp]
theorem eventually_mem_nhds {s : Set Î±} {a : Î±} : (âˆ€á¶  x in ğ“ a, s âˆˆ ğ“ x) â†” s âˆˆ ğ“ a :=
  eventually_eventually_nhds
#align eventually_mem_nhds eventually_mem_nhds

@[simp]
theorem nhds_bind_nhds : (ğ“ a).bind ğ“ = ğ“ a :=
  Filter.ext fun _ => eventually_eventually_nhds
#align nhds_bind_nhds nhds_bind_nhds

@[simp]
theorem eventually_eventuallyEq_nhds {f g : Î± â†’ Î²} {a : Î±} :
    (âˆ€á¶  y in ğ“ a, f =á¶ [ğ“ y] g) â†” f =á¶ [ğ“ a] g :=
  eventually_eventually_nhds
#align eventually_eventually_eq_nhds eventually_eventuallyEq_nhds

theorem Filter.EventuallyEq.eq_of_nhds {f g : Î± â†’ Î²} {a : Î±} (h : f =á¶ [ğ“ a] g) : f a = g a :=
  h.self_of_nhds
#align filter.eventually_eq.eq_of_nhds Filter.EventuallyEq.eq_of_nhds

@[simp]
theorem eventually_eventuallyLE_nhds [LE Î²] {f g : Î± â†’ Î²} {a : Î±} :
    (âˆ€á¶  y in ğ“ a, f â‰¤á¶ [ğ“ y] g) â†” f â‰¤á¶ [ğ“ a] g :=
  eventually_eventually_nhds
#align eventually_eventually_le_nhds eventually_eventuallyLE_nhds

/-- If two functions are equal in a neighbourhood of `a`, then for `y` sufficiently close
to `a` these functions are equal in a neighbourhood of `y`. -/
theorem Filter.EventuallyEq.eventuallyEq_nhds {f g : Î± â†’ Î²} {a : Î±} (h : f =á¶ [ğ“ a] g) :
    âˆ€á¶  y in ğ“ a, f =á¶ [ğ“ y] g :=
  h.eventually_nhds
#align filter.eventually_eq.eventually_eq_nhds Filter.EventuallyEq.eventuallyEq_nhds

/-- If `f x â‰¤ g x` in a neighbourhood of `a`, then for `y` sufficiently close to `a` we have
`f x â‰¤ g x` in a neighbourhood of `y`. -/
theorem Filter.EventuallyLE.eventuallyLE_nhds [LE Î²] {f g : Î± â†’ Î²} {a : Î±} (h : f â‰¤á¶ [ğ“ a] g) :
    âˆ€á¶  y in ğ“ a, f â‰¤á¶ [ğ“ y] g :=
  h.eventually_nhds
#align filter.eventually_le.eventually_le_nhds Filter.EventuallyLE.eventuallyLE_nhds

theorem all_mem_nhds (x : Î±) (P : Set Î± â†’ Prop) (hP : âˆ€ s t, s âŠ† t â†’ P s â†’ P t) :
    (âˆ€ s âˆˆ ğ“ x, P s) â†” âˆ€ s, IsOpen s â†’ x âˆˆ s â†’ P s :=
  ((nhds_basis_opens x).forall_iff hP).trans <| by simp only [@and_comm (x âˆˆ _), and_imp]
#align all_mem_nhds all_mem_nhds

theorem all_mem_nhds_filter (x : Î±) (f : Set Î± â†’ Set Î²) (hf : âˆ€ s t, s âŠ† t â†’ f s âŠ† f t)
    (l : Filter Î²) : (âˆ€ s âˆˆ ğ“ x, f s âˆˆ l) â†” âˆ€ s, IsOpen s â†’ x âˆˆ s â†’ f s âˆˆ l :=
  all_mem_nhds _ _ fun s t ssubt h => mem_of_superset h (hf s t ssubt)
#align all_mem_nhds_filter all_mem_nhds_filter

theorem tendsto_nhds {f : Î² â†’ Î±} {l : Filter Î²} {a : Î±} :
    Tendsto f l (ğ“ a) â†” âˆ€ s, IsOpen s â†’ a âˆˆ s â†’ f â»Â¹' s âˆˆ l :=
  all_mem_nhds_filter _ _ (fun _ _ h => preimage_mono h) _
#align tendsto_nhds tendsto_nhds

theorem tendsto_atTop_nhds [Nonempty Î²] [SemilatticeSup Î²] {f : Î² â†’ Î±} {a : Î±} :
    Tendsto f atTop (ğ“ a) â†” âˆ€ U : Set Î±, a âˆˆ U â†’ IsOpen U â†’ âˆƒ N, âˆ€ n, N â‰¤ n â†’ f n âˆˆ U :=
  (atTop_basis.tendsto_iff (nhds_basis_opens a)).trans <| by
    simp only [and_imp, exists_prop, true_and_iff, mem_Ici, ge_iff_le]
#align tendsto_at_top_nhds tendsto_atTop_nhds

theorem tendsto_const_nhds {a : Î±} {f : Filter Î²} : Tendsto (fun _ : Î² => a) f (ğ“ a) :=
  tendsto_nhds.mpr fun _ _ ha => univ_mem' fun _ => ha
#align tendsto_const_nhds tendsto_const_nhds

theorem tendsto_atTop_of_eventually_const {Î¹ : Type*} [SemilatticeSup Î¹] [Nonempty Î¹] {x : Î±}
    {u : Î¹ â†’ Î±} {iâ‚€ : Î¹} (h : âˆ€ i â‰¥ iâ‚€, u i = x) : Tendsto u atTop (ğ“ x) :=
  Tendsto.congr' (EventuallyEq.symm (eventually_atTop.mpr âŸ¨iâ‚€, hâŸ©)) tendsto_const_nhds
#align tendsto_at_top_of_eventually_const tendsto_atTop_of_eventually_const

theorem tendsto_atBot_of_eventually_const {Î¹ : Type*} [SemilatticeInf Î¹] [Nonempty Î¹] {x : Î±}
    {u : Î¹ â†’ Î±} {iâ‚€ : Î¹} (h : âˆ€ i â‰¤ iâ‚€, u i = x) : Tendsto u atBot (ğ“ x) :=
  Tendsto.congr' (EventuallyEq.symm (eventually_atBot.mpr âŸ¨iâ‚€, hâŸ©)) tendsto_const_nhds
#align tendsto_at_bot_of_eventually_const tendsto_atBot_of_eventually_const

theorem pure_le_nhds : pure â‰¤ (ğ“ : Î± â†’ Filter Î±) := fun _ _ hs => mem_pure.2 <| mem_of_mem_nhds hs
#align pure_le_nhds pure_le_nhds

theorem tendsto_pure_nhds {Î± : Type*} [TopologicalSpace Î²] (f : Î± â†’ Î²) (a : Î±) :
    Tendsto f (pure a) (ğ“ (f a)) :=
  (tendsto_pure_pure f a).mono_right (pure_le_nhds _)
#align tendsto_pure_nhds tendsto_pure_nhds

theorem OrderTop.tendsto_atTop_nhds {Î± : Type*} [PartialOrder Î±] [OrderTop Î±] [TopologicalSpace Î²]
    (f : Î± â†’ Î²) : Tendsto f atTop (ğ“ (f âŠ¤)) :=
  (tendsto_atTop_pure f).mono_right (pure_le_nhds _)
#align order_top.tendsto_at_top_nhds OrderTop.tendsto_atTop_nhds

@[simp]
instance nhds_neBot {a : Î±} : NeBot (ğ“ a) :=
  neBot_of_le (pure_le_nhds a)
#align nhds_ne_bot nhds_neBot

theorem tendsto_nhds_of_eventually_eq {f : Î² â†’ Î±} {a : Î±} (h : âˆ€á¶  x in l, f x = a) :
    Tendsto f l (ğ“ a) :=
  tendsto_const_nhds.congr' (.symm h)

/-!
### Cluster points

In this section we define [cluster points](https://en.wikipedia.org/wiki/Limit_point)
(also known as limit points and accumulation points) of a filter and of a sequence.
-/


/-- A point `x` is a cluster point of a filter `F` if `ğ“ x âŠ“ F â‰  âŠ¥`. Also known as
an accumulation point or a limit point, but beware that terminology varies. This
is *not* the same as asking `ğ“[â‰ ] x âŠ“ F â‰  âŠ¥`. See `mem_closure_iff_clusterPt` in particular. -/
def ClusterPt (x : Î±) (F : Filter Î±) : Prop :=
  NeBot (ğ“ x âŠ“ F)
#align cluster_pt ClusterPt

theorem ClusterPt.neBot {x : Î±} {F : Filter Î±} (h : ClusterPt x F) : NeBot (ğ“ x âŠ“ F) :=
  h
#align cluster_pt.ne_bot ClusterPt.neBot

theorem Filter.HasBasis.clusterPt_iff {Î¹a Î¹F} {pa : Î¹a â†’ Prop} {sa : Î¹a â†’ Set Î±} {pF : Î¹F â†’ Prop}
    {sF : Î¹F â†’ Set Î±} {F : Filter Î±} (ha : (ğ“ a).HasBasis pa sa) (hF : F.HasBasis pF sF) :
    ClusterPt a F â†” âˆ€ â¦ƒiâ¦„, pa i â†’ âˆ€ â¦ƒjâ¦„, pF j â†’ (sa i âˆ© sF j).Nonempty :=
  ha.inf_basis_neBot_iff hF
#align filter.has_basis.cluster_pt_iff Filter.HasBasis.clusterPt_iff

theorem clusterPt_iff {x : Î±} {F : Filter Î±} :
    ClusterPt x F â†” âˆ€ â¦ƒU : Set Î±â¦„, U âˆˆ ğ“ x â†’ âˆ€ â¦ƒVâ¦„, V âˆˆ F â†’ (U âˆ© V).Nonempty :=
  inf_neBot_iff
#align cluster_pt_iff clusterPt_iff

theorem clusterPt_iff_not_disjoint {x : Î±} {F : Filter Î±} :
    ClusterPt x F â†” Â¬Disjoint (ğ“ x) F := by
  rw [disjoint_iff, ClusterPt, neBot_iff]

/-- `x` is a cluster point of a set `s` if every neighbourhood of `x` meets `s` on a nonempty
set. See also `mem_closure_iff_clusterPt`. -/
theorem clusterPt_principal_iff {x : Î±} {s : Set Î±} :
    ClusterPt x (ğ“Ÿ s) â†” âˆ€ U âˆˆ ğ“ x, (U âˆ© s).Nonempty :=
  inf_principal_neBot_iff
#align cluster_pt_principal_iff clusterPt_principal_iff

theorem clusterPt_principal_iff_frequently {x : Î±} {s : Set Î±} :
    ClusterPt x (ğ“Ÿ s) â†” âˆƒá¶  y in ğ“ x, y âˆˆ s := by
  simp only [clusterPt_principal_iff, frequently_iff, Set.Nonempty, exists_prop, mem_inter_iff]
#align cluster_pt_principal_iff_frequently clusterPt_principal_iff_frequently

theorem ClusterPt.of_le_nhds {x : Î±} {f : Filter Î±} (H : f â‰¤ ğ“ x) [NeBot f] : ClusterPt x f := by
  rwa [ClusterPt, inf_eq_right.mpr H]
#align cluster_pt.of_le_nhds ClusterPt.of_le_nhds

theorem ClusterPt.of_le_nhds' {x : Î±} {f : Filter Î±} (H : f â‰¤ ğ“ x) (_hf : NeBot f) :
    ClusterPt x f :=
  ClusterPt.of_le_nhds H
#align cluster_pt.of_le_nhds' ClusterPt.of_le_nhds'

theorem ClusterPt.of_nhds_le {x : Î±} {f : Filter Î±} (H : ğ“ x â‰¤ f) : ClusterPt x f := by
  simp only [ClusterPt, inf_eq_left.mpr H, nhds_neBot]
#align cluster_pt.of_nhds_le ClusterPt.of_nhds_le

theorem ClusterPt.mono {x : Î±} {f g : Filter Î±} (H : ClusterPt x f) (h : f â‰¤ g) : ClusterPt x g :=
  NeBot.mono H <| inf_le_inf_left _ h
#align cluster_pt.mono ClusterPt.mono

theorem ClusterPt.of_inf_left {x : Î±} {f g : Filter Î±} (H : ClusterPt x <| f âŠ“ g) : ClusterPt x f :=
  H.mono inf_le_left
#align cluster_pt.of_inf_left ClusterPt.of_inf_left

theorem ClusterPt.of_inf_right {x : Î±} {f g : Filter Î±} (H : ClusterPt x <| f âŠ“ g) :
    ClusterPt x g :=
  H.mono inf_le_right
#align cluster_pt.of_inf_right ClusterPt.of_inf_right

theorem Ultrafilter.clusterPt_iff {x : Î±} {f : Ultrafilter Î±} : ClusterPt x f â†” â†‘f â‰¤ ğ“ x :=
  âŸ¨f.le_of_inf_neBot', fun h => ClusterPt.of_le_nhds hâŸ©
#align ultrafilter.cluster_pt_iff Ultrafilter.clusterPt_iff

/-- A point `x` is a cluster point of a sequence `u` along a filter `F` if it is a cluster point
of `map u F`. -/
def MapClusterPt {Î¹ : Type*} (x : Î±) (F : Filter Î¹) (u : Î¹ â†’ Î±) : Prop :=
  ClusterPt x (map u F)
#align map_cluster_pt MapClusterPt

theorem mapClusterPt_iff {Î¹ : Type*} (x : Î±) (F : Filter Î¹) (u : Î¹ â†’ Î±) :
    MapClusterPt x F u â†” âˆ€ s âˆˆ ğ“ x, âˆƒá¶  a in F, u a âˆˆ s := by
  simp_rw [MapClusterPt, ClusterPt, inf_neBot_iff_frequently_left, frequently_map]
  rfl
#align map_cluster_pt_iff mapClusterPt_iff

theorem mapClusterPt_of_comp {Î¹ Î´ : Type*} {F : Filter Î¹} {Ï† : Î´ â†’ Î¹} {p : Filter Î´} {x : Î±}
    {u : Î¹ â†’ Î±} [NeBot p] (h : Tendsto Ï† p F) (H : Tendsto (u âˆ˜ Ï†) p (ğ“ x)) :
    MapClusterPt x F u := by
  have :=
    calc
      map (u âˆ˜ Ï†) p = map u (map Ï† p) := map_map
      _ â‰¤ map u F := map_mono h
  have : map (u âˆ˜ Ï†) p â‰¤ ğ“ x âŠ“ map u F := le_inf H this
  exact neBot_of_le this
#align map_cluster_pt_of_comp mapClusterPt_of_comp

/-- A point `x` is an accumulation point of a filter `F` if `ğ“[â‰ ] x âŠ“ F â‰  âŠ¥`.-/
def AccPt (x : Î±) (F : Filter Î±) : Prop :=
  NeBot (ğ“[â‰ ] x âŠ“ F)
#align acc_pt AccPt

theorem acc_iff_cluster (x : Î±) (F : Filter Î±) : AccPt x F â†” ClusterPt x (ğ“Ÿ {x}á¶œ âŠ“ F) := by
  rw [AccPt, nhdsWithin, ClusterPt, inf_assoc]
#align acc_iff_cluster acc_iff_cluster

/-- `x` is an accumulation point of a set `C` iff it is a cluster point of `C âˆ– {x}`.-/
theorem acc_principal_iff_cluster (x : Î±) (C : Set Î±) : AccPt x (ğ“Ÿ C) â†” ClusterPt x (ğ“Ÿ (C \ {x})) :=
  by rw [acc_iff_cluster, inf_principal, inter_comm]; rfl
#align acc_principal_iff_cluster acc_principal_iff_cluster

/-- `x` is an accumulation point of a set `C` iff every neighborhood
of `x` contains a point of `C` other than `x`. -/
theorem accPt_iff_nhds (x : Î±) (C : Set Î±) : AccPt x (ğ“Ÿ C) â†” âˆ€ U âˆˆ ğ“ x, âˆƒ y âˆˆ U âˆ© C, y â‰  x := by
  simp [acc_principal_iff_cluster, clusterPt_principal_iff, Set.Nonempty, exists_prop, and_assoc,
    @and_comm (Â¬_ = x)]
#align acc_pt_iff_nhds accPt_iff_nhds

/-- `x` is an accumulation point of a set `C` iff
there are points near `x` in `C` and different from `x`.-/
theorem accPt_iff_frequently (x : Î±) (C : Set Î±) : AccPt x (ğ“Ÿ C) â†” âˆƒá¶  y in ğ“ x, y â‰  x âˆ§ y âˆˆ C := by
  simp [acc_principal_iff_cluster, clusterPt_principal_iff_frequently, and_comm]
#align acc_pt_iff_frequently accPt_iff_frequently

/-- If `x` is an accumulation point of `F` and `F â‰¤ G`, then
`x` is an accumulation point of `D`. -/
theorem AccPt.mono {x : Î±} {F G : Filter Î±} (h : AccPt x F) (hFG : F â‰¤ G) : AccPt x G :=
  NeBot.mono h (inf_le_inf_left _ hFG)
#align acc_pt.mono AccPt.mono

/-!
### Interior, closure and frontier in terms of neighborhoods
-/

theorem interior_eq_nhds' {s : Set Î±} : interior s = { a | s âˆˆ ğ“ a } :=
  Set.ext fun x => by simp only [mem_interior, mem_nhds_iff, mem_setOf_eq]
#align interior_eq_nhds' interior_eq_nhds'

theorem interior_eq_nhds {s : Set Î±} : interior s = { a | ğ“ a â‰¤ ğ“Ÿ s } :=
  interior_eq_nhds'.trans <| by simp only [le_principal_iff]
#align interior_eq_nhds interior_eq_nhds

@[simp]
theorem interior_mem_nhds {s : Set Î±} {a : Î±} : interior s âˆˆ ğ“ a â†” s âˆˆ ğ“ a :=
  âŸ¨fun h => mem_of_superset h interior_subset, fun h =>
    IsOpen.mem_nhds isOpen_interior (mem_interior_iff_mem_nhds.2 h)âŸ©
#align interior_mem_nhds interior_mem_nhds

theorem interior_setOf_eq {p : Î± â†’ Prop} : interior { x | p x } = { x | âˆ€á¶  y in ğ“ x, p y } :=
  interior_eq_nhds'
#align interior_set_of_eq interior_setOf_eq

theorem isOpen_setOf_eventually_nhds {p : Î± â†’ Prop} : IsOpen { x | âˆ€á¶  y in ğ“ x, p y } := by
  simp only [â† interior_setOf_eq, isOpen_interior]
#align is_open_set_of_eventually_nhds isOpen_setOf_eventually_nhds

theorem subset_interior_iff_nhds {s V : Set Î±} : s âŠ† interior V â†” âˆ€ x âˆˆ s, V âˆˆ ğ“ x := by
  simp_rw [subset_def, mem_interior_iff_mem_nhds]
#align subset_interior_iff_nhds subset_interior_iff_nhds

theorem isOpen_iff_nhds {s : Set Î±} : IsOpen s â†” âˆ€ a âˆˆ s, ğ“ a â‰¤ ğ“Ÿ s :=
  calc
    IsOpen s â†” s âŠ† interior s := subset_interior_iff_isOpen.symm
    _ â†” âˆ€ a âˆˆ s, ğ“ a â‰¤ ğ“Ÿ s := by rw [interior_eq_nhds]; rfl
#align is_open_iff_nhds isOpen_iff_nhds

theorem isOpen_iff_mem_nhds {s : Set Î±} : IsOpen s â†” âˆ€ a âˆˆ s, s âˆˆ ğ“ a :=
  isOpen_iff_nhds.trans <| forall_congr' fun _ => imp_congr_right fun _ => le_principal_iff
#align is_open_iff_mem_nhds isOpen_iff_mem_nhds

/-- A set `s` is open iff for every point `x` in `s` and every `y` close to `x`, `y` is in `s`. -/
theorem isOpen_iff_eventually {s : Set Î±} : IsOpen s â†” âˆ€ x, x âˆˆ s â†’ âˆ€á¶  y in ğ“ x, y âˆˆ s :=
  isOpen_iff_mem_nhds
#align is_open_iff_eventually isOpen_iff_eventually

theorem isOpen_iff_ultrafilter {s : Set Î±} :
    IsOpen s â†” âˆ€ x âˆˆ s, âˆ€ (l : Ultrafilter Î±), â†‘l â‰¤ ğ“ x â†’ s âˆˆ l := by
  simp_rw [isOpen_iff_mem_nhds, â† mem_iff_ultrafilter]
#align is_open_iff_ultrafilter isOpen_iff_ultrafilter

theorem isOpen_singleton_iff_nhds_eq_pure (a : Î±) : IsOpen ({a} : Set Î±) â†” ğ“ a = pure a := by
  constructor
  Â· intro h
    apply le_antisymm _ (pure_le_nhds a)
    rw [le_pure_iff]
    exact h.mem_nhds (mem_singleton a)
  Â· intro h
    simp [isOpen_iff_nhds, h]
#align is_open_singleton_iff_nhds_eq_pure isOpen_singleton_iff_nhds_eq_pure

theorem isOpen_singleton_iff_punctured_nhds {Î± : Type*} [TopologicalSpace Î±] (a : Î±) :
    IsOpen ({a} : Set Î±) â†” ğ“[â‰ ] a = âŠ¥ := by
  rw [isOpen_singleton_iff_nhds_eq_pure, nhdsWithin, â† mem_iff_inf_principal_compl, â† le_pure_iff,
    nhds_neBot.le_pure_iff]
#align is_open_singleton_iff_punctured_nhds isOpen_singleton_iff_punctured_nhds

theorem mem_closure_iff_frequently {s : Set Î±} {a : Î±} : a âˆˆ closure s â†” âˆƒá¶  x in ğ“ a, x âˆˆ s := by
  rw [Filter.Frequently, Filter.Eventually, â† mem_interior_iff_mem_nhds,
      closure_eq_compl_interior_compl]; rfl
#align mem_closure_iff_frequently mem_closure_iff_frequently

alias âŸ¨_, Filter.Frequently.mem_closureâŸ© := mem_closure_iff_frequently
#align filter.frequently.mem_closure Filter.Frequently.mem_closure

/-- A set `s` is closed iff for every point `x`, if there is a point `y` close to `x` that belongs
to `s` then `x` is in `s`. -/
theorem isClosed_iff_frequently {s : Set Î±} : IsClosed s â†” âˆ€ x, (âˆƒá¶  y in ğ“ x, y âˆˆ s) â†’ x âˆˆ s := by
  rw [â† closure_subset_iff_isClosed]
  refine' forall_congr' fun x => _
  rw [mem_closure_iff_frequently]
#align is_closed_iff_frequently isClosed_iff_frequently

/-- The set of cluster points of a filter is closed. In particular, the set of limit points
of a sequence is closed. -/
theorem isClosed_setOf_clusterPt {f : Filter Î±} : IsClosed { x | ClusterPt x f } := by
  simp only [ClusterPt, inf_neBot_iff_frequently_left, setOf_forall, imp_iff_not_or]
  refine' isClosed_iInter fun p => IsClosed.union _ _ <;> apply isClosed_compl_iff.2
  exacts [isOpen_setOf_eventually_nhds, isOpen_const]
#align is_closed_set_of_cluster_pt isClosed_setOf_clusterPt

theorem mem_closure_iff_clusterPt {s : Set Î±} {a : Î±} : a âˆˆ closure s â†” ClusterPt a (ğ“Ÿ s) :=
  mem_closure_iff_frequently.trans clusterPt_principal_iff_frequently.symm
#align mem_closure_iff_cluster_pt mem_closure_iff_clusterPt

theorem mem_closure_iff_nhds_neBot {s : Set Î±} : a âˆˆ closure s â†” ğ“ a âŠ“ ğ“Ÿ s â‰  âŠ¥ :=
  mem_closure_iff_clusterPt.trans neBot_iff
#align mem_closure_iff_nhds_ne_bot mem_closure_iff_nhds_neBot

theorem mem_closure_iff_nhdsWithin_neBot {s : Set Î±} {x : Î±} : x âˆˆ closure s â†” NeBot (ğ“[s] x) :=
  mem_closure_iff_clusterPt
#align mem_closure_iff_nhds_within_ne_bot mem_closure_iff_nhdsWithin_neBot

/-- If `x` is not an isolated point of a topological space, then `{x}á¶œ` is dense in the whole
space. -/
theorem dense_compl_singleton (x : Î±) [NeBot (ğ“[â‰ ] x)] : Dense ({x}á¶œ : Set Î±) := by
  intro y
  rcases eq_or_ne y x with (rfl | hne)
  Â· rwa [mem_closure_iff_nhdsWithin_neBot]
  Â· exact subset_closure hne
#align dense_compl_singleton dense_compl_singleton

/-- If `x` is not an isolated point of a topological space, then the closure of `{x}á¶œ` is the whole
space. -/
-- porting note: was a `@[simp]` lemma but `simp` can prove it
theorem closure_compl_singleton (x : Î±) [NeBot (ğ“[â‰ ] x)] : closure {x}á¶œ = (univ : Set Î±) :=
  (dense_compl_singleton x).closure_eq
#align closure_compl_singleton closure_compl_singleton

/-- If `x` is not an isolated point of a topological space, then the interior of `{x}` is empty. -/
@[simp]
theorem interior_singleton (x : Î±) [NeBot (ğ“[â‰ ] x)] : interior {x} = (âˆ… : Set Î±) :=
  interior_eq_empty_iff_dense_compl.2 (dense_compl_singleton x)
#align interior_singleton interior_singleton

theorem not_isOpen_singleton (x : Î±) [NeBot (ğ“[â‰ ] x)] : Â¬IsOpen ({x} : Set Î±) :=
  dense_compl_singleton_iff_not_open.1 (dense_compl_singleton x)
#align not_is_open_singleton not_isOpen_singleton

theorem closure_eq_cluster_pts {s : Set Î±} : closure s = { a | ClusterPt a (ğ“Ÿ s) } :=
  Set.ext fun _ => mem_closure_iff_clusterPt
#align closure_eq_cluster_pts closure_eq_cluster_pts

theorem mem_closure_iff_nhds {s : Set Î±} {a : Î±} : a âˆˆ closure s â†” âˆ€ t âˆˆ ğ“ a, (t âˆ© s).Nonempty :=
  mem_closure_iff_clusterPt.trans clusterPt_principal_iff
#align mem_closure_iff_nhds mem_closure_iff_nhds

theorem mem_closure_iff_nhds' {s : Set Î±} {a : Î±} : a âˆˆ closure s â†” âˆ€ t âˆˆ ğ“ a, âˆƒ y : s, â†‘y âˆˆ t := by
  simp only [mem_closure_iff_nhds, Set.inter_nonempty_iff_exists_right, SetCoe.exists, exists_prop]
#align mem_closure_iff_nhds' mem_closure_iff_nhds'

theorem mem_closure_iff_comap_neBot {A : Set Î±} {x : Î±} :
    x âˆˆ closure A â†” NeBot (comap ((â†‘) : A â†’ Î±) (ğ“ x)) := by
  simp_rw [mem_closure_iff_nhds, comap_neBot_iff, Set.inter_nonempty_iff_exists_right,
    SetCoe.exists, exists_prop]
#align mem_closure_iff_comap_ne_bot mem_closure_iff_comap_neBot

theorem mem_closure_iff_nhds_basis' {a : Î±} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set Î±} (h : (ğ“ a).HasBasis p s)
    {t : Set Î±} : a âˆˆ closure t â†” âˆ€ i, p i â†’ (s i âˆ© t).Nonempty :=
  mem_closure_iff_clusterPt.trans <|
    (h.clusterPt_iff (hasBasis_principal _)).trans <| by simp only [exists_prop, forall_const]
#align mem_closure_iff_nhds_basis' mem_closure_iff_nhds_basis'

theorem mem_closure_iff_nhds_basis {a : Î±} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set Î±} (h : (ğ“ a).HasBasis p s)
    {t : Set Î±} : a âˆˆ closure t â†” âˆ€ i, p i â†’ âˆƒ y âˆˆ t, y âˆˆ s i :=
  (mem_closure_iff_nhds_basis' h).trans <| by
    simp only [Set.Nonempty, mem_inter_iff, exists_prop, and_comm]
#align mem_closure_iff_nhds_basis mem_closure_iff_nhds_basis

theorem clusterPt_iff_forall_mem_closure {F : Filter Î±} {a : Î±} :
    ClusterPt a F â†” âˆ€ s âˆˆ F, a âˆˆ closure s := by
  simp_rw [ClusterPt, inf_neBot_iff, mem_closure_iff_nhds]
  rw [forallâ‚‚_swap]

theorem clusterPt_iff_lift'_closure {F : Filter Î±} {a : Î±} :
    ClusterPt a F â†” pure a â‰¤ (F.lift' closure) := by
  simp_rw [clusterPt_iff_forall_mem_closure,
    (hasBasis_pure _).le_basis_iff F.basis_sets.lift'_closure, id, singleton_subset_iff, true_and,
    exists_const]

theorem clusterPt_iff_lift'_closure' {F : Filter Î±} {a : Î±} :
    ClusterPt a F â†” (F.lift' closure âŠ“ pure a).NeBot := by
  rw [clusterPt_iff_lift'_closure, â† Ultrafilter.coe_pure, inf_comm, Ultrafilter.inf_neBot_iff]

@[simp]
theorem clusterPt_lift'_closure_iff {F : Filter Î±} {a : Î±} :
    ClusterPt a (F.lift' closure) â†” ClusterPt a F := by
  simp [clusterPt_iff_lift'_closure, lift'_lift'_assoc (monotone_closure Î±) (monotone_closure Î±)]

/-- `x` belongs to the closure of `s` if and only if some ultrafilter
  supported on `s` converges to `x`. -/
theorem mem_closure_iff_ultrafilter {s : Set Î±} {x : Î±} :
    x âˆˆ closure s â†” âˆƒ u : Ultrafilter Î±, s âˆˆ u âˆ§ â†‘u â‰¤ ğ“ x := by
  simp [closure_eq_cluster_pts, ClusterPt, â† exists_ultrafilter_iff, and_comm]
#align mem_closure_iff_ultrafilter mem_closure_iff_ultrafilter

theorem isClosed_iff_clusterPt {s : Set Î±} : IsClosed s â†” âˆ€ a, ClusterPt a (ğ“Ÿ s) â†’ a âˆˆ s :=
  calc
    IsClosed s â†” closure s âŠ† s := closure_subset_iff_isClosed.symm
    _ â†” âˆ€ a, ClusterPt a (ğ“Ÿ s) â†’ a âˆˆ s := by simp only [subset_def, mem_closure_iff_clusterPt]
#align is_closed_iff_cluster_pt isClosed_iff_clusterPt

theorem isClosed_iff_nhds {s : Set Î±} : IsClosed s â†” âˆ€ x, (âˆ€ U âˆˆ ğ“ x, (U âˆ© s).Nonempty) â†’ x âˆˆ s :=
  by simp_rw [isClosed_iff_clusterPt, ClusterPt, inf_principal_neBot_iff]
#align is_closed_iff_nhds isClosed_iff_nhds

theorem IsClosed.interior_union_left {s t : Set Î±} (_ : IsClosed s) :
    interior (s âˆª t) âŠ† s âˆª interior t := fun a âŸ¨u, âŸ¨âŸ¨huâ‚, huâ‚‚âŸ©, haâŸ©âŸ© =>
  (Classical.em (a âˆˆ s)).imp_right fun h =>
    mem_interior.mpr
      âŸ¨u âˆ© sá¶œ, fun _x hx => (huâ‚‚ hx.1).resolve_left hx.2, IsOpen.inter huâ‚ IsClosed.isOpen_compl,
        âŸ¨ha, hâŸ©âŸ©
#align is_closed.interior_union_left IsClosed.interior_union_left

theorem IsClosed.interior_union_right {s t : Set Î±} (h : IsClosed t) :
    interior (s âˆª t) âŠ† interior s âˆª t := by
  simpa only [union_comm _ t] using h.interior_union_left
#align is_closed.interior_union_right IsClosed.interior_union_right

theorem IsOpen.inter_closure {s t : Set Î±} (h : IsOpen s) : s âˆ© closure t âŠ† closure (s âˆ© t) :=
  compl_subset_compl.mp <| by
    simpa only [â† interior_compl, compl_inter] using IsClosed.interior_union_left h.isClosed_compl
#align is_open.inter_closure IsOpen.inter_closure

theorem IsOpen.closure_inter {s t : Set Î±} (h : IsOpen t) : closure s âˆ© t âŠ† closure (s âˆ© t) := by
  simpa only [inter_comm t] using h.inter_closure
#align is_open.closure_inter IsOpen.closure_inter

theorem Dense.open_subset_closure_inter {s t : Set Î±} (hs : Dense s) (ht : IsOpen t) :
    t âŠ† closure (t âˆ© s) :=
  calc
    t = t âˆ© closure s := by rw [hs.closure_eq, inter_univ]
    _ âŠ† closure (t âˆ© s) := ht.inter_closure
#align dense.open_subset_closure_inter Dense.open_subset_closure_inter

theorem mem_closure_of_mem_closure_union {sâ‚ sâ‚‚ : Set Î±} {x : Î±} (h : x âˆˆ closure (sâ‚ âˆª sâ‚‚))
    (hâ‚ : sâ‚á¶œ âˆˆ ğ“ x) : x âˆˆ closure sâ‚‚ := by
  rw [mem_closure_iff_nhds_neBot] at *
  rwa [â†
    calc
      ğ“ x âŠ“ principal (sâ‚ âˆª sâ‚‚) = ğ“ x âŠ“ (principal sâ‚ âŠ” principal sâ‚‚) := by rw [sup_principal]
      _ = ğ“ x âŠ“ principal sâ‚ âŠ” ğ“ x âŠ“ principal sâ‚‚ := inf_sup_left
      _ = âŠ¥ âŠ” ğ“ x âŠ“ principal sâ‚‚ := by rw [inf_principal_eq_bot.mpr hâ‚]
      _ = ğ“ x âŠ“ principal sâ‚‚ := bot_sup_eq
      ]
#align mem_closure_of_mem_closure_union mem_closure_of_mem_closure_union

/-- The intersection of an open dense set with a dense set is a dense set. -/
theorem Dense.inter_of_open_left {s t : Set Î±} (hs : Dense s) (ht : Dense t) (hso : IsOpen s) :
    Dense (s âˆ© t) := fun x =>
  closure_minimal hso.inter_closure isClosed_closure <| by simp [hs.closure_eq, ht.closure_eq]
#align dense.inter_of_open_left Dense.inter_of_open_left

/-- The intersection of a dense set with an open dense set is a dense set. -/
theorem Dense.inter_of_open_right {s t : Set Î±} (hs : Dense s) (ht : Dense t) (hto : IsOpen t) :
    Dense (s âˆ© t) :=
  inter_comm t s â–¸ ht.inter_of_open_left hs hto
#align dense.inter_of_open_right Dense.inter_of_open_right

theorem Dense.inter_nhds_nonempty {s t : Set Î±} (hs : Dense s) {x : Î±} (ht : t âˆˆ ğ“ x) :
    (s âˆ© t).Nonempty :=
  let âŸ¨U, hsub, ho, hxâŸ© := mem_nhds_iff.1 ht
  (hs.inter_open_nonempty U ho âŸ¨x, hxâŸ©).mono fun _y hy => âŸ¨hy.2, hsub hy.1âŸ©
#align dense.inter_nhds_nonempty Dense.inter_nhds_nonempty

theorem closure_diff {s t : Set Î±} : closure s \ closure t âŠ† closure (s \ t) :=
  calc
    closure s \ closure t = (closure t)á¶œ âˆ© closure s := by simp only [diff_eq, inter_comm]
    _ âŠ† closure ((closure t)á¶œ âˆ© s) := (isOpen_compl_iff.mpr <| isClosed_closure).inter_closure
    _ = closure (s \ closure t) := by simp only [diff_eq, inter_comm]
    _ âŠ† closure (s \ t) := closure_mono <| diff_subset_diff (Subset.refl s) subset_closure
#align closure_diff closure_diff

theorem Filter.Frequently.mem_of_closed {a : Î±} {s : Set Î±} (h : âˆƒá¶  x in ğ“ a, x âˆˆ s)
    (hs : IsClosed s) : a âˆˆ s :=
  hs.closure_subset h.mem_closure
#align filter.frequently.mem_of_closed Filter.Frequently.mem_of_closed

theorem IsClosed.mem_of_frequently_of_tendsto {f : Î² â†’ Î±} {b : Filter Î²} {a : Î±} {s : Set Î±}
    (hs : IsClosed s) (h : âˆƒá¶  x in b, f x âˆˆ s) (hf : Tendsto f b (ğ“ a)) : a âˆˆ s :=
  (hf.frequently <| show âˆƒá¶  x in b, (fun y => y âˆˆ s) (f x) from h).mem_of_closed hs
#align is_closed.mem_of_frequently_of_tendsto IsClosed.mem_of_frequently_of_tendsto

theorem IsClosed.mem_of_tendsto {f : Î² â†’ Î±} {b : Filter Î²} {a : Î±} {s : Set Î±} [NeBot b]
    (hs : IsClosed s) (hf : Tendsto f b (ğ“ a)) (h : âˆ€á¶  x in b, f x âˆˆ s) : a âˆˆ s :=
  hs.mem_of_frequently_of_tendsto h.frequently hf
#align is_closed.mem_of_tendsto IsClosed.mem_of_tendsto

theorem mem_closure_of_frequently_of_tendsto {f : Î² â†’ Î±} {b : Filter Î²} {a : Î±} {s : Set Î±}
    (h : âˆƒá¶  x in b, f x âˆˆ s) (hf : Tendsto f b (ğ“ a)) : a âˆˆ closure s :=
  (hf.frequently h).mem_closure
#align mem_closure_of_frequently_of_tendsto mem_closure_of_frequently_of_tendsto

theorem mem_closure_of_tendsto {f : Î² â†’ Î±} {b : Filter Î²} {a : Î±} {s : Set Î±} [NeBot b]
    (hf : Tendsto f b (ğ“ a)) (h : âˆ€á¶  x in b, f x âˆˆ s) : a âˆˆ closure s :=
  mem_closure_of_frequently_of_tendsto h.frequently hf
#align mem_closure_of_tendsto mem_closure_of_tendsto

/-- Suppose that `f` sends the complement to `s` to a single point `a`, and `l` is some filter.
Then `f` tends to `a` along `l` restricted to `s` if and only if it tends to `a` along `l`. -/
theorem tendsto_inf_principal_nhds_iff_of_forall_eq {f : Î² â†’ Î±} {l : Filter Î²} {s : Set Î²} {a : Î±}
    (h : âˆ€ (x) (_ : x âˆ‰ s), f x = a) : Tendsto f (l âŠ“ ğ“Ÿ s) (ğ“ a) â†” Tendsto f l (ğ“ a) := by
  rw [tendsto_iff_comap, tendsto_iff_comap]
  replace h : ğ“Ÿ sá¶œ â‰¤ comap f (ğ“ a)
  Â· rintro U âŸ¨t, ht, htUâŸ© x hx
    have : f x âˆˆ t := (h x hx).symm â–¸ mem_of_mem_nhds ht
    exact htU this
  refine' âŸ¨fun h' => _, le_trans inf_le_leftâŸ©
  have := sup_le h' h
  rw [sup_inf_right, sup_principal, union_compl_self, principal_univ, inf_top_eq, sup_le_iff]
    at this
  exact this.1
#align tendsto_inf_principal_nhds_iff_of_forall_eq tendsto_inf_principal_nhds_iff_of_forall_eq

/-!
### Limits of filters in topological spaces

In this section we define functions that return a limit of a filter (or of a function along a
filter), if it exists, and a random point otherwise. These functions are rarely used in Mathlib,
most of the theorems are written using `Filter.Tendsto`. One of the reasons is that
`Filter.limUnder f g = a` is not equivalent to `Filter.Tendsto g f (ğ“ a)` unless the codomain is a
Hausdorff space and `g` has a limit along `f`.
-/

section lim

-- "Lim"
set_option linter.uppercaseLean3 false

/-- If `f` is a filter, then `Filter.lim f` is a limit of the filter, if it exists. -/
noncomputable def lim [Nonempty Î±] (f : Filter Î±) : Î± :=
  Classical.epsilon fun a => f â‰¤ ğ“ a
#align Lim lim

/--
If `F` is an ultrafilter, then `Filter.Ultrafilter.lim F` is a limit of the filter, if it exists.
Note that dot notation `F.lim` can be used for `F : Filter.Ultrafilter Î±`.
-/
noncomputable nonrec def Ultrafilter.lim (F : Ultrafilter Î±) : Î± :=
  @lim Î± _ (nonempty_of_neBot F) F
#align ultrafilter.Lim Ultrafilter.lim

/-- If `f` is a filter in `Î²` and `g : Î² â†’ Î±` is a function, then `limUnder f g` is a limit of `g`
at `f`, if it exists. -/
noncomputable def limUnder [Nonempty Î±] (f : Filter Î²) (g : Î² â†’ Î±) : Î± :=
  lim (f.map g)
#align lim limUnder

/-- If a filter `f` is majorated by some `ğ“ a`, then it is majorated by `ğ“ (Filter.lim f)`. We
formulate this lemma with a `[Nonempty Î±]` argument of `lim` derived from `h` to make it useful for
types without a `[Nonempty Î±]` instance. Because of the built-in proof irrelevance, Lean will unify
this instance with any other instance. -/
theorem le_nhds_lim {f : Filter Î±} (h : âˆƒ a, f â‰¤ ğ“ a) : f â‰¤ ğ“ (@lim _ _ (nonempty_of_exists h) f) :=
  Classical.epsilon_spec h
#align le_nhds_Lim le_nhds_lim

/-- If `g` tends to some `ğ“ a` along `f`, then it tends to `ğ“ (Filter.limUnder f g)`. We formulate
this lemma with a `[Nonempty Î±]` argument of `lim` derived from `h` to make it useful for types
without a `[Nonempty Î±]` instance. Because of the built-in proof irrelevance, Lean will unify this
instance with any other instance. -/
theorem tendsto_nhds_limUnder {f : Filter Î²} {g : Î² â†’ Î±} (h : âˆƒ a, Tendsto g f (ğ“ a)) :
    Tendsto g f (ğ“ (@limUnder _ _ _ (nonempty_of_exists h) f g)) :=
  le_nhds_lim h
#align tendsto_nhds_lim tendsto_nhds_limUnder

end lim

end TopologicalSpace

open Topology

/-!
### Continuity
-/

section Continuous

variable {Î± : Type*} {Î² : Type*} {Î³ : Type*} {Î´ : Type*}

variable [TopologicalSpace Î±] [TopologicalSpace Î²] [TopologicalSpace Î³]

open TopologicalSpace

/-- A function between topological spaces is continuous if the preimage
  of every open set is open. Registered as a structure to make sure it is not unfolded by Lean. -/
structure Continuous (f : Î± â†’ Î²) : Prop where
  /-- The preimage of an open set under a continuous function is an open set. Use `IsOpen.preimage`
  instead. -/
  isOpen_preimage : âˆ€ s, IsOpen s â†’ IsOpen (f â»Â¹' s)
#align continuous Continuous

set_option quotPrecheck false in
/-- Notation for `Continuous` with respect to a non-standard topologies. -/
scoped[Topology] notation (name := Continuous_of) "Continuous[" tâ‚ ", " tâ‚‚ "]" =>
  @Continuous _ _ tâ‚ tâ‚‚

theorem continuous_def {_ : TopologicalSpace Î±} {_ : TopologicalSpace Î²} {f : Î± â†’ Î²} :
    Continuous f â†” âˆ€ s, IsOpen s â†’ IsOpen (f â»Â¹' s) :=
  âŸ¨fun hf => hf.1, fun h => âŸ¨hâŸ©âŸ©
#align continuous_def continuous_def

theorem IsOpen.preimage {f : Î± â†’ Î²} (hf : Continuous f) {s : Set Î²} (h : IsOpen s) :
    IsOpen (f â»Â¹' s) :=
  hf.isOpen_preimage s h
#align is_open.preimage IsOpen.preimage

theorem Continuous.congr {f g : Î± â†’ Î²} (h : Continuous f) (h' : âˆ€ x, f x = g x) : Continuous g := by
  convert h
  ext
  rw [h']
#align continuous.congr Continuous.congr

/-- A function between topological spaces is continuous at a point `xâ‚€`
if `f x` tends to `f xâ‚€` when `x` tends to `xâ‚€`. -/
def ContinuousAt (f : Î± â†’ Î²) (x : Î±) :=
  Tendsto f (ğ“ x) (ğ“ (f x))
#align continuous_at ContinuousAt

theorem ContinuousAt.tendsto {f : Î± â†’ Î²} {x : Î±} (h : ContinuousAt f x) :
    Tendsto f (ğ“ x) (ğ“ (f x)) :=
  h
#align continuous_at.tendsto ContinuousAt.tendsto

theorem continuousAt_def {f : Î± â†’ Î²} {x : Î±} : ContinuousAt f x â†” âˆ€ A âˆˆ ğ“ (f x), f â»Â¹' A âˆˆ ğ“ x :=
  Iff.rfl
#align continuous_at_def continuousAt_def

theorem continuousAt_congr {f g : Î± â†’ Î²} {x : Î±} (h : f =á¶ [ğ“ x] g) :
    ContinuousAt f x â†” ContinuousAt g x := by
  simp only [ContinuousAt, tendsto_congr' h, h.eq_of_nhds]
#align continuous_at_congr continuousAt_congr

theorem ContinuousAt.congr {f g : Î± â†’ Î²} {x : Î±} (hf : ContinuousAt f x) (h : f =á¶ [ğ“ x] g) :
    ContinuousAt g x :=
  (continuousAt_congr h).1 hf
#align continuous_at.congr ContinuousAt.congr

theorem ContinuousAt.preimage_mem_nhds {f : Î± â†’ Î²} {x : Î±} {t : Set Î²} (h : ContinuousAt f x)
    (ht : t âˆˆ ğ“ (f x)) : f â»Â¹' t âˆˆ ğ“ x :=
  h ht
#align continuous_at.preimage_mem_nhds ContinuousAt.preimage_mem_nhds

theorem eventuallyEq_zero_nhds {Mâ‚€} [Zero Mâ‚€] {a : Î±} {f : Î± â†’ Mâ‚€} :
    f =á¶ [ğ“ a] 0 â†” a âˆ‰ closure (Function.support f) := by
  rw [â† mem_compl_iff, â† interior_compl, mem_interior_iff_mem_nhds, Function.compl_support]; rfl
#align eventually_eq_zero_nhds eventuallyEq_zero_nhds

theorem ClusterPt.map {x : Î±} {la : Filter Î±} {lb : Filter Î²} (H : ClusterPt x la) {f : Î± â†’ Î²}
    (hfc : ContinuousAt f x) (hf : Tendsto f la lb) : ClusterPt (f x) lb :=
  (NeBot.map H f).mono <| hfc.tendsto.inf hf
#align cluster_pt.map ClusterPt.map

/-- See also `interior_preimage_subset_preimage_interior`. -/
theorem preimage_interior_subset_interior_preimage {f : Î± â†’ Î²} {s : Set Î²} (hf : Continuous f) :
    f â»Â¹' interior s âŠ† interior (f â»Â¹' s) :=
  interior_maximal (preimage_mono interior_subset) (isOpen_interior.preimage hf)
#align preimage_interior_subset_interior_preimage preimage_interior_subset_interior_preimage

@[continuity]
theorem continuous_id : Continuous (id : Î± â†’ Î±) :=
  continuous_def.2 fun _ => id
#align continuous_id continuous_id

-- This is needed due to reducibility issues with the `continuity` tactic.
@[continuity]
theorem continuous_id' : Continuous (fun (x : Î±) => x) := continuous_id

theorem Continuous.comp {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : Continuous g) (hf : Continuous f) :
    Continuous (g âˆ˜ f) :=
  continuous_def.2 fun _ h => (h.preimage hg).preimage hf
#align continuous.comp Continuous.comp

-- This is needed due to reducibility issues with the `continuity` tactic.
@[continuity]
theorem Continuous.comp' {g : Î² â†’ Î³} {f : Î± â†’ Î²} (hg : Continuous g) (hf : Continuous f) :
    Continuous (fun x => g (f x)) := hg.comp hf

theorem Continuous.iterate {f : Î± â†’ Î±} (h : Continuous f) (n : â„•) : Continuous f^[n] :=
  Nat.recOn n continuous_id fun _ ihn => ihn.comp h
#align continuous.iterate Continuous.iterate

nonrec theorem ContinuousAt.comp {g : Î² â†’ Î³} {f : Î± â†’ Î²} {x : Î±} (hg : ContinuousAt g (f x))
    (hf : ContinuousAt f x) : ContinuousAt (g âˆ˜ f) x :=
  hg.comp hf
#align continuous_at.comp ContinuousAt.comp

/-- See note [comp_of_eq lemmas] -/
theorem ContinuousAt.comp_of_eq {g : Î² â†’ Î³} {f : Î± â†’ Î²} {x : Î±} {y : Î²} (hg : ContinuousAt g y)
    (hf : ContinuousAt f x) (hy : f x = y) : ContinuousAt (g âˆ˜ f) x := by subst hy; exact hg.comp hf
#align continuous_at.comp_of_eq ContinuousAt.comp_of_eq

theorem Continuous.tendsto {f : Î± â†’ Î²} (hf : Continuous f) (x) : Tendsto f (ğ“ x) (ğ“ (f x)) :=
  ((nhds_basis_opens x).tendsto_iff <| nhds_basis_opens <| f x).2 fun t âŸ¨hxt, htâŸ© =>
    âŸ¨f â»Â¹' t, âŸ¨hxt, ht.preimage hfâŸ©, Subset.rflâŸ©
#align continuous.tendsto Continuous.tendsto

/-- A version of `Continuous.tendsto` that allows one to specify a simpler form of the limit.
E.g., one can write `continuous_exp.tendsto' 0 1 exp_zero`. -/
theorem Continuous.tendsto' {f : Î± â†’ Î²} (hf : Continuous f) (x : Î±) (y : Î²) (h : f x = y) :
    Tendsto f (ğ“ x) (ğ“ y) :=
  h â–¸ hf.tendsto x
#align continuous.tendsto' Continuous.tendsto'

theorem Continuous.continuousAt {f : Î± â†’ Î²} {x : Î±} (h : Continuous f) : ContinuousAt f x :=
  h.tendsto x
#align continuous.continuous_at Continuous.continuousAt

theorem continuous_iff_continuousAt {f : Î± â†’ Î²} : Continuous f â†” âˆ€ x, ContinuousAt f x :=
  âŸ¨Continuous.tendsto, fun hf => continuous_def.2 fun _U hU => isOpen_iff_mem_nhds.2 fun x hx =>
    hf x <| hU.mem_nhds hxâŸ©
#align continuous_iff_continuous_at continuous_iff_continuousAt

theorem continuousAt_const {x : Î±} {b : Î²} : ContinuousAt (fun _ : Î± => b) x :=
  tendsto_const_nhds
#align continuous_at_const continuousAt_const

@[continuity]
theorem continuous_const {b : Î²} : Continuous fun _ : Î± => b :=
  continuous_iff_continuousAt.mpr fun _ => continuousAt_const
#align continuous_const continuous_const

theorem Filter.EventuallyEq.continuousAt {x : Î±} {f : Î± â†’ Î²} {y : Î²} (h : f =á¶ [ğ“ x] fun _ => y) :
    ContinuousAt f x :=
  (continuousAt_congr h).2 tendsto_const_nhds
#align filter.eventually_eq.continuous_at Filter.EventuallyEq.continuousAt

theorem continuous_of_const {f : Î± â†’ Î²} (h : âˆ€ x y, f x = f y) : Continuous f :=
  continuous_iff_continuousAt.mpr fun x =>
    Filter.EventuallyEq.continuousAt <| eventually_of_forall fun y => h y x
#align continuous_of_const continuous_of_const

theorem continuousAt_id {x : Î±} : ContinuousAt id x :=
  continuous_id.continuousAt
#align continuous_at_id continuousAt_id

theorem ContinuousAt.iterate {f : Î± â†’ Î±} {x : Î±} (hf : ContinuousAt f x) (hx : f x = x) (n : â„•) :
    ContinuousAt f^[n] x :=
  Nat.recOn n continuousAt_id fun n ihn =>
    show ContinuousAt (f^[n] âˆ˜ f) x from ContinuousAt.comp (hx.symm â–¸ ihn) hf
#align continuous_at.iterate ContinuousAt.iterate

theorem continuous_iff_isClosed {f : Î± â†’ Î²} : Continuous f â†” âˆ€ s, IsClosed s â†’ IsClosed (f â»Â¹' s) :=
  continuous_def.trans <| compl_surjective.forall.trans <| by
    simp only [isOpen_compl_iff, preimage_compl]
#align continuous_iff_is_closed continuous_iff_isClosed

theorem IsClosed.preimage {f : Î± â†’ Î²} (hf : Continuous f) {s : Set Î²} (h : IsClosed s) :
    IsClosed (f â»Â¹' s) :=
  continuous_iff_isClosed.mp hf s h
#align is_closed.preimage IsClosed.preimage

theorem mem_closure_image {f : Î± â†’ Î²} {x : Î±} {s : Set Î±} (hf : ContinuousAt f x)
    (hx : x âˆˆ closure s) : f x âˆˆ closure (f '' s) :=
  mem_closure_of_frequently_of_tendsto
    ((mem_closure_iff_frequently.1 hx).mono fun _ => mem_image_of_mem _) hf
#align mem_closure_image mem_closure_image

theorem continuousAt_iff_ultrafilter {f : Î± â†’ Î²} {x} :
    ContinuousAt f x â†” âˆ€ g : Ultrafilter Î±, â†‘g â‰¤ ğ“ x â†’ Tendsto f g (ğ“ (f x)) :=
  tendsto_iff_ultrafilter f (ğ“ x) (ğ“ (f x))
#align continuous_at_iff_ultrafilter continuousAt_iff_ultrafilter

theorem continuous_iff_ultrafilter {f : Î± â†’ Î²} :
    Continuous f â†” âˆ€ (x) (g : Ultrafilter Î±), â†‘g â‰¤ ğ“ x â†’ Tendsto f g (ğ“ (f x)) := by
  simp only [continuous_iff_continuousAt, continuousAt_iff_ultrafilter]
#align continuous_iff_ultrafilter continuous_iff_ultrafilter

theorem Continuous.closure_preimage_subset {f : Î± â†’ Î²} (hf : Continuous f) (t : Set Î²) :
    closure (f â»Â¹' t) âŠ† f â»Â¹' closure t := by
  rw [â† (isClosed_closure.preimage hf).closure_eq]
  exact closure_mono (preimage_mono subset_closure)
#align continuous.closure_preimage_subset Continuous.closure_preimage_subset

theorem Continuous.frontier_preimage_subset {f : Î± â†’ Î²} (hf : Continuous f) (t : Set Î²) :
    frontier (f â»Â¹' t) âŠ† f â»Â¹' frontier t :=
  diff_subset_diff (hf.closure_preimage_subset t) (preimage_interior_subset_interior_preimage hf)
#align continuous.frontier_preimage_subset Continuous.frontier_preimage_subset

/-- If a continuous map `f` maps `s` to `t`, then it maps `closure s` to `closure t`. -/
protected theorem Set.MapsTo.closure {s : Set Î±} {t : Set Î²} {f : Î± â†’ Î²} (h : MapsTo f s t)
    (hc : Continuous f) : MapsTo f (closure s) (closure t) := by
  simp only [MapsTo, mem_closure_iff_clusterPt]
  exact fun x hx => hx.map hc.continuousAt (tendsto_principal_principal.2 h)
#align set.maps_to.closure Set.MapsTo.closure

/-- See also `IsClosedMap.closure_image_eq_of_continuous`. -/
theorem image_closure_subset_closure_image {f : Î± â†’ Î²} {s : Set Î±} (h : Continuous f) :
    f '' closure s âŠ† closure (f '' s) :=
  ((mapsTo_image f s).closure h).image_subset
#align image_closure_subset_closure_image image_closure_subset_closure_image

-- porting note: new lemma
theorem closure_image_closure {f : Î± â†’ Î²} {s : Set Î±} (h : Continuous f) :
    closure (f '' closure s) = closure (f '' s) :=
  Subset.antisymm
    (closure_minimal (image_closure_subset_closure_image h) isClosed_closure)
    (closure_mono <| image_subset _ subset_closure)

theorem closure_subset_preimage_closure_image {f : Î± â†’ Î²} {s : Set Î±} (h : Continuous f) :
    closure s âŠ† f â»Â¹' closure (f '' s) := by
  rw [â† Set.image_subset_iff]
  exact image_closure_subset_closure_image h
#align closure_subset_preimage_closure_image closure_subset_preimage_closure_image

theorem map_mem_closure {s : Set Î±} {t : Set Î²} {f : Î± â†’ Î²} {a : Î±} (hf : Continuous f)
    (ha : a âˆˆ closure s) (ht : MapsTo f s t) : f a âˆˆ closure t :=
  ht.closure hf ha
#align map_mem_closure map_mem_closure

/-- If a continuous map `f` maps `s` to a closed set `t`, then it maps `closure s` to `t`. -/
theorem Set.MapsTo.closure_left {s : Set Î±} {t : Set Î²} {f : Î± â†’ Î²} (h : MapsTo f s t)
    (hc : Continuous f) (ht : IsClosed t) : MapsTo f (closure s) t :=
  ht.closure_eq â–¸ h.closure hc
#align set.maps_to.closure_left Set.MapsTo.closure_left

/-!
### Function with dense range
-/

section DenseRange

variable {Îº Î¹ : Type*} (f : Îº â†’ Î²) (g : Î² â†’ Î³)

/-- `f : Î¹ â†’ Î²` has dense range if its range (image) is a dense subset of Î². -/
def DenseRange := Dense (range f)
#align dense_range DenseRange

variable {f}

/-- A surjective map has dense range. -/
theorem Function.Surjective.denseRange (hf : Function.Surjective f) : DenseRange f := fun x => by
  simp [hf.range_eq]
#align function.surjective.dense_range Function.Surjective.denseRange

theorem denseRange_id : DenseRange (id : Î± â†’ Î±) :=
  Function.Surjective.denseRange Function.surjective_id
#align dense_range_id denseRange_id

theorem denseRange_iff_closure_range : DenseRange f â†” closure (range f) = univ :=
  dense_iff_closure_eq
#align dense_range_iff_closure_range denseRange_iff_closure_range

theorem DenseRange.closure_range (h : DenseRange f) : closure (range f) = univ :=
  h.closure_eq
#align dense_range.closure_range DenseRange.closure_range

theorem Dense.denseRange_val {s : Set Î±} (h : Dense s) : DenseRange ((â†‘) : s â†’ Î±) := by
  simpa only [DenseRange, Subtype.range_coe_subtype]
#align dense.dense_range_coe Dense.denseRange_val

theorem Continuous.range_subset_closure_image_dense {f : Î± â†’ Î²} (hf : Continuous f) {s : Set Î±}
    (hs : Dense s) : range f âŠ† closure (f '' s) := by
  rw [â† image_univ, â† hs.closure_eq]
  exact image_closure_subset_closure_image hf
#align continuous.range_subset_closure_image_dense Continuous.range_subset_closure_image_dense

/-- The image of a dense set under a continuous map with dense range is a dense set. -/
theorem DenseRange.dense_image {f : Î± â†’ Î²} (hf' : DenseRange f) (hf : Continuous f) {s : Set Î±}
    (hs : Dense s) : Dense (f '' s) :=
  (hf'.mono <| hf.range_subset_closure_image_dense hs).of_closure
#align dense_range.dense_image DenseRange.dense_image

/-- If `f` has dense range and `s` is an open set in the codomain of `f`, then the image of the
preimage of `s` under `f` is dense in `s`. -/
theorem DenseRange.subset_closure_image_preimage_of_isOpen (hf : DenseRange f) {s : Set Î²}
    (hs : IsOpen s) : s âŠ† closure (f '' (f â»Â¹' s)) := by
  rw [image_preimage_eq_inter_range]
  exact hf.open_subset_closure_inter hs
#align dense_range.subset_closure_image_preimage_of_is_open DenseRange.subset_closure_image_preimage_of_isOpen

/-- If a continuous map with dense range maps a dense set to a subset of `t`, then `t` is a dense
set. -/
theorem DenseRange.dense_of_mapsTo {f : Î± â†’ Î²} (hf' : DenseRange f) (hf : Continuous f) {s : Set Î±}
    (hs : Dense s) {t : Set Î²} (ht : MapsTo f s t) : Dense t :=
  (hf'.dense_image hf hs).mono ht.image_subset
#align dense_range.dense_of_maps_to DenseRange.dense_of_mapsTo

/-- Composition of a continuous map with dense range and a function with dense range has dense
range. -/
theorem DenseRange.comp {g : Î² â†’ Î³} {f : Îº â†’ Î²} (hg : DenseRange g) (hf : DenseRange f)
    (cg : Continuous g) : DenseRange (g âˆ˜ f) := by
  rw [DenseRange, range_comp]
  exact hg.dense_image cg hf
#align dense_range.comp DenseRange.comp

nonrec theorem DenseRange.nonempty_iff (hf : DenseRange f) : Nonempty Îº â†” Nonempty Î² :=
  range_nonempty_iff_nonempty.symm.trans hf.nonempty_iff
#align dense_range.nonempty_iff DenseRange.nonempty_iff

theorem DenseRange.nonempty [h : Nonempty Î²] (hf : DenseRange f) : Nonempty Îº :=
  hf.nonempty_iff.mpr h
#align dense_range.nonempty DenseRange.nonempty

/-- Given a function `f : Î± â†’ Î²` with dense range and `b : Î²`, returns some `a : Î±`. -/
def DenseRange.some (hf : DenseRange f) (b : Î²) : Îº :=
  Classical.choice <| hf.nonempty_iff.mpr âŸ¨bâŸ©
#align dense_range.some DenseRange.some

nonrec theorem DenseRange.exists_mem_open (hf : DenseRange f) {s : Set Î²} (ho : IsOpen s)
    (hs : s.Nonempty) : âˆƒ a, f a âˆˆ s :=
  exists_range_iff.1 <| hf.exists_mem_open ho hs
#align dense_range.exists_mem_open DenseRange.exists_mem_open

theorem DenseRange.mem_nhds {f : Îº â†’ Î²} (h : DenseRange f) {b : Î²} {U : Set Î²} (U_in : U âˆˆ ğ“ b) :
    âˆƒ a, f a âˆˆ U :=
  let âŸ¨a, haâŸ© := h.exists_mem_open isOpen_interior âŸ¨b, mem_interior_iff_mem_nhds.2 U_inâŸ©
  âŸ¨a, interior_subset haâŸ©
#align dense_range.mem_nhds DenseRange.mem_nhds

end DenseRange

end Continuous

library_note "continuity lemma statement"/--
The library contains many lemmas stating that functions/operations are continuous. There are many
ways to formulate the continuity of operations. Some are more convenient than others.
Note: for the most part this note also applies to other properties
(`Measurable`, `Differentiable`, `ContinuousOn`, ...).

### The traditional way
As an example, let's look at addition `(+) : M â†’ M â†’ M`. We can state that this is continuous
in different definitionally equal ways (omitting some typing information)
* `Continuous (fun p â†¦ p.1 + p.2)`;
* `Continuous (Function.uncurry (+))`;
* `Continuous â†¿(+)`. (`â†¿` is notation for recursively uncurrying a function)

However, lemmas with this conclusion are not nice to use in practice because
1. They confuse the elaborator. The following two examples fail, because of limitations in the
  elaboration process.
  ```
  variables {M : Type*} [Add M] [TopologicalSpace M] [ContinuousAdd M]
  example : Continuous (Î» x : M, x + x) :=
  continuous_add.comp _

  example : Continuous (Î» x : M, x + x) :=
  continuous_add.comp (continuous_id.prod_mk continuous_id)
  ```
  The second is a valid proof, which is accepted if you write it as
  `continuous_add.comp (continuous_id.prod_mk continuous_id : _)`

2. If the operation has more than 2 arguments, they are impractical to use, because in your
  application the arguments in the domain might be in a different order or associated differently.

### The convenient way

A much more convenient way to write continuity lemmas is like `Continuous.add`:
```
Continuous.add {f g : X â†’ M} (hf : Continuous f) (hg : Continuous g) : Continuous (Î» x, f x + g x)
```
The conclusion can be `Continuous (f + g)`, which is definitionally equal.
This has the following advantages
* It supports projection notation, so is shorter to write.
* `Continuous.add _ _` is recognized correctly by the elaborator and gives useful new goals.
* It works generally, since the domain is a variable.

As an example for a unary operation, we have `Continuous.neg`.
```
Continuous.neg {f : Î± â†’ G} (hf : Continuous f) : Continuous (fun x â†¦ -f x)
```
For unary functions, the elaborator is not confused when applying the traditional lemma
(like `continuous_neg`), but it's still convenient to have the short version available (compare
`hf.neg.neg.neg` with `continuous_neg.comp <| continuous_neg.comp <| continuous_neg.comp hf`).

As a harder example, consider an operation of the following type:
```
def strans {x : F} (Î³ Î³' : Path x x) (tâ‚€ : I) : Path x x
```
The precise definition is not important, only its type.
The correct continuity principle for this operation is something like this:
```
{f : X â†’ F} {Î³ Î³' : âˆ€ x, Path (f x) (f x)} {tâ‚€ s : X â†’ I}
  (hÎ³ : Continuous â†¿Î³) (hÎ³' : Continuous â†¿Î³')
  (ht : Continuous tâ‚€) (hs : Continuous s) :
  Continuous (fun x â†¦ strans (Î³ x) (Î³' x) (t x) (s x))
```
Note that *all* arguments of `strans` are indexed over `X`, even the basepoint `x`, and the last
argument `s` that arises since `Path x x` has a coercion to `I â†’ F`. The paths `Î³` and `Î³'` (which
are unary functions from `I`) become binary functions in the continuity lemma.

### Summary
* Make sure that your continuity lemmas are stated in the most general way, and in a convenient
  form. That means that:
  - The conclusion has a variable `X` as domain (not something like `Y Ã— Z`);
  - Wherever possible, all point arguments `c : Y` are replaced by functions `c : X â†’ Y`;
  - All `n`-ary function arguments are replaced by `n+1`-ary functions
    (`f : Y â†’ Z` becomes `f : X â†’ Y â†’ Z`);
  - All (relevant) arguments have continuity assumptions, and perhaps there are additional
    assumptions needed to make the operation continuous;
  - The function in the conclusion is fully applied.
* These remarks are mostly about the format of the *conclusion* of a continuity lemma.
  In assumptions it's fine to state that a function with more than 1 argument is continuous using
  `â†¿` or `Function.uncurry`.

### Functions with discontinuities

In some cases, you want to work with discontinuous functions, and in certain expressions they are
still continuous. For example, consider the fractional part of a number, `Int.fract : â„ â†’ â„`.
In this case, you want to add conditions to when a function involving `fract` is continuous, so you
get something like this: (assumption `hf` could be weakened, but the important thing is the shape
of the conclusion)
```
lemma ContinuousOn.comp_fract {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
  {f : X â†’ â„ â†’ Y} {g : X â†’ â„} (hf : Continuous â†¿f) (hg : Continuous g) (h : âˆ€ s, f s 0 = f s 1) :
  Continuous (fun x â†¦ f x (fract (g x)))
```
With `ContinuousAt` you can be even more precise about what to prove in case of discontinuities,
see e.g. `ContinuousAt.comp_div_cases`.
-/

library_note "comp_of_eq lemmas"/--
Lean's elaborator has trouble elaborating applications of lemmas that state that the composition of
two functions satisfy some property at a point, like `ContinuousAt.comp` / `ContDiffAt.comp` and
`ContMDiffWithinAt.comp`. The reason is that a lemma like this looks like
`ContinuousAt g (f x) â†’ ContinuousAt f x â†’ ContinuousAt (g âˆ˜ f) x`.
Since Lean's elaborator elaborates the arguments from left-to-right, when you write `hg.comp hf`,
the elaborator will try to figure out *both* `f` and `g` from the type of `hg`. It tries to figure
out `f` just from the point where `g` is continuous. For example, if `hg : ContinuousAt g (a, x)`
then the elaborator will assign `f` to the function `Prod.mk a`, since in that case `f x = (a, x)`.
This is undesirable in most cases where `f` is not a variable. There are some ways to work around
this, for example by giving `f` explicitly, or to force Lean to elaborate `hf` before elaborating
`hg`, but this is annoying.
Another better solution is to reformulate composition lemmas to have the following shape
`ContinuousAt g y â†’ ContinuousAt f x â†’ f x = y â†’ ContinuousAt (g âˆ˜ f) x`.
This is even useful if the proof of `f x = y` is `rfl`.
The reason that this works better is because the type of `hg` doesn't mention `f`.
Only after elaborating the two `ContinuousAt` arguments, Lean will try to unify `f x` with `y`,
which is often easy after having chosen the correct functions for `f` and `g`.
Here is an example that shows the difference:
```
example [TopologicalSpace Î±] [TopologicalSpace Î²] {xâ‚€ : Î±} (f : Î± â†’ Î± â†’ Î²)
    (hf : ContinuousAt (Function.uncurry f) (xâ‚€, xâ‚€)) :
    ContinuousAt (fun x â†¦ f x x) xâ‚€ :=
  -- hf.comp (continuousAt_id.prod continuousAt_id) -- type mismatch
  -- hf.comp_of_eq (continuousAt_id.prod continuousAt_id) rfl -- works
```
-/
