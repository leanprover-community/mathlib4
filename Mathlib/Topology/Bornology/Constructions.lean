/-
Copyright (c) 2022 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import Mathlib.Topology.Bornology.Basic

#align_import topology.bornology.constructions from "leanprover-community/mathlib"@"e3d9ab8faa9dea8f78155c6c27d62a621f4c152d"

/-!
# Bornology structure on products and subtypes

In this file we define `Bornology` and `BoundedSpace` instances on `Œ± √ó Œ≤`, `Œ† i, œÄ i`, and
`{x // p x}`. We also prove basic lemmas about `Bornology.cobounded` and `Bornology.IsBounded`
on these types.
-/


open Set Filter Bornology Function

open Filter

variable {Œ± Œ≤ Œπ : Type*} {œÄ : Œπ ‚Üí Type*} [Fintype Œπ] [Bornology Œ±] [Bornology Œ≤]
  [‚àÄ i, Bornology (œÄ i)]

instance Prod.instBornology : Bornology (Œ± √ó Œ≤) where
  cobounded' := (cobounded Œ±).coprod (cobounded Œ≤)
  le_cofinite' :=
    @coprod_cofinite Œ± Œ≤ ‚ñ∏ coprod_mono ‚ÄπBornology Œ±‚Ä∫.le_cofinite ‚ÄπBornology Œ≤‚Ä∫.le_cofinite
#align prod.bornology Prod.instBornology

instance Pi.instBornology : Bornology (‚àÄ i, œÄ i) where
  cobounded' := Filter.coprod·µ¢ fun i => cobounded (œÄ i)
  le_cofinite' := @coprod·µ¢_cofinite Œπ œÄ _ ‚ñ∏ Filter.coprod·µ¢_mono fun _ => Bornology.le_cofinite _
#align pi.bornology Pi.instBornology

/-- Inverse image of a bornology. -/
@[reducible]
def Bornology.induced {Œ± Œ≤ : Type*} [Bornology Œ≤] (f : Œ± ‚Üí Œ≤) : Bornology Œ±
    where
  cobounded' := comap f (cobounded Œ≤)
  le_cofinite' := (comap_mono (Bornology.le_cofinite Œ≤)).trans (comap_cofinite_le _)
#align bornology.induced Bornology.induced

instance {p : Œ± ‚Üí Prop} : Bornology (Subtype p) :=
  Bornology.induced (Subtype.val : Subtype p ‚Üí Œ±)

namespace Bornology

/-!
### Bounded sets in `Œ± √ó Œ≤`
-/


theorem cobounded_prod : cobounded (Œ± √ó Œ≤) = (cobounded Œ±).coprod (cobounded Œ≤) :=
  rfl
#align bornology.cobounded_prod Bornology.cobounded_prod

theorem isBounded_image_fst_and_snd {s : Set (Œ± √ó Œ≤)} :
    IsBounded (Prod.fst '' s) ‚àß IsBounded (Prod.snd '' s) ‚Üî IsBounded s :=
  compl_mem_coprod.symm
#align bornology.is_bounded_image_fst_and_snd Bornology.isBounded_image_fst_and_snd

variable {s : Set Œ±} {t : Set Œ≤} {S : ‚àÄ i, Set (œÄ i)}

theorem IsBounded.fst_of_prod (h : IsBounded (s √óÀ¢ t)) (ht : t.Nonempty) : IsBounded s :=
  fst_image_prod s ht ‚ñ∏ (isBounded_image_fst_and_snd.2 h).1
#align bornology.is_bounded.fst_of_prod Bornology.IsBounded.fst_of_prod

theorem IsBounded.snd_of_prod (h : IsBounded (s √óÀ¢ t)) (hs : s.Nonempty) : IsBounded t :=
  snd_image_prod hs t ‚ñ∏ (isBounded_image_fst_and_snd.2 h).2
#align bornology.is_bounded.snd_of_prod Bornology.IsBounded.snd_of_prod

theorem IsBounded.prod (hs : IsBounded s) (ht : IsBounded t) : IsBounded (s √óÀ¢ t) :=
  isBounded_image_fst_and_snd.1
    ‚ü®hs.subset <| fst_image_prod_subset _ _, ht.subset <| snd_image_prod_subset _ _‚ü©
#align bornology.is_bounded.prod Bornology.IsBounded.prod

theorem isBounded_prod_of_nonempty (hne : Set.Nonempty (s √óÀ¢ t)) :
    IsBounded (s √óÀ¢ t) ‚Üî IsBounded s ‚àß IsBounded t :=
  ‚ü®fun h => ‚ü®h.fst_of_prod hne.snd, h.snd_of_prod hne.fst‚ü©, fun h => h.1.prod h.2‚ü©
#align bornology.is_bounded_prod_of_nonempty Bornology.isBounded_prod_of_nonempty

theorem isBounded_prod : IsBounded (s √óÀ¢ t) ‚Üî s = ‚àÖ ‚à® t = ‚àÖ ‚à® IsBounded s ‚àß IsBounded t := by
  rcases s.eq_empty_or_nonempty with (rfl | hs); ¬∑ simp
  -- ‚ä¢ IsBounded (‚àÖ √óÀ¢ t) ‚Üî ‚àÖ = ‚àÖ ‚à® t = ‚àÖ ‚à® IsBounded ‚àÖ ‚àß IsBounded t
                                                   -- üéâ no goals
  rcases t.eq_empty_or_nonempty with (rfl | ht); ¬∑ simp
  -- ‚ä¢ IsBounded (s √óÀ¢ ‚àÖ) ‚Üî s = ‚àÖ ‚à® ‚àÖ = ‚àÖ ‚à® IsBounded s ‚àß IsBounded ‚àÖ
                                                   -- üéâ no goals
  simp only [hs.ne_empty, ht.ne_empty, isBounded_prod_of_nonempty (hs.prod ht), false_or_iff]
  -- üéâ no goals
#align bornology.is_bounded_prod Bornology.isBounded_prod

theorem isBounded_prod_self : IsBounded (s √óÀ¢ s) ‚Üî IsBounded s := by
  rcases s.eq_empty_or_nonempty with (rfl | hs); ¬∑ simp
  -- ‚ä¢ IsBounded (‚àÖ √óÀ¢ ‚àÖ) ‚Üî IsBounded ‚àÖ
                                                   -- üéâ no goals
  exact (isBounded_prod_of_nonempty (hs.prod hs)).trans (and_self_iff _)
  -- üéâ no goals
#align bornology.is_bounded_prod_self Bornology.isBounded_prod_self

/-!
### Bounded sets in `Œ† i, œÄ i`
-/


theorem cobounded_pi : cobounded (‚àÄ i, œÄ i) = Filter.coprod·µ¢ fun i => cobounded (œÄ i) :=
  rfl
#align bornology.cobounded_pi Bornology.cobounded_pi

theorem forall_isBounded_image_eval_iff {s : Set (‚àÄ i, œÄ i)} :
    (‚àÄ i, IsBounded (eval i '' s)) ‚Üî IsBounded s :=
  compl_mem_coprod·µ¢.symm
#align bornology.forall_is_bounded_image_eval_iff Bornology.forall_isBounded_image_eval_iff

theorem IsBounded.pi (h : ‚àÄ i, IsBounded (S i)) : IsBounded (pi univ S) :=
  forall_isBounded_image_eval_iff.1 fun i => (h i).subset eval_image_univ_pi_subset
#align bornology.is_bounded.pi Bornology.IsBounded.pi

theorem isBounded_pi_of_nonempty (hne : (pi univ S).Nonempty) :
    IsBounded (pi univ S) ‚Üî ‚àÄ i, IsBounded (S i) :=
  ‚ü®fun H i => @eval_image_univ_pi _ _ _ i hne ‚ñ∏ forall_isBounded_image_eval_iff.2 H i, IsBounded.pi‚ü©
#align bornology.is_bounded_pi_of_nonempty Bornology.isBounded_pi_of_nonempty

theorem isBounded_pi : IsBounded (pi univ S) ‚Üî (‚àÉ i, S i = ‚àÖ) ‚à® ‚àÄ i, IsBounded (S i) := by
  by_cases hne : ‚àÉ i, S i = ‚àÖ
  -- ‚ä¢ IsBounded (Set.pi univ S) ‚Üî (‚àÉ i, S i = ‚àÖ) ‚à® ‚àÄ (i : Œπ), IsBounded (S i)
  ¬∑ simp [hne, univ_pi_eq_empty_iff.2 hne]
    -- üéâ no goals
  ¬∑ simp only [hne, false_or_iff]
    -- ‚ä¢ IsBounded (Set.pi univ S) ‚Üî ‚àÄ (i : Œπ), IsBounded (S i)
    simp only [not_exists, ‚Üê Ne.def, ‚Üê nonempty_iff_ne_empty, ‚Üê univ_pi_nonempty_iff] at hne
    -- ‚ä¢ IsBounded (Set.pi univ S) ‚Üî ‚àÄ (i : Œπ), IsBounded (S i)
    exact isBounded_pi_of_nonempty hne
    -- üéâ no goals
#align bornology.is_bounded_pi Bornology.isBounded_pi

/-!
### Bounded sets in `{x // p x}`
-/


theorem isBounded_induced {Œ± Œ≤ : Type*} [Bornology Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ±} :
    @IsBounded Œ± (Bornology.induced f) s ‚Üî IsBounded (f '' s) :=
  compl_mem_comap
#align bornology.is_bounded_induced Bornology.isBounded_induced

theorem isBounded_image_subtype_val {p : Œ± ‚Üí Prop} {s : Set { x // p x }} :
    IsBounded (Subtype.val '' s) ‚Üî IsBounded s :=
  isBounded_induced.symm
#align bornology.is_bounded_image_subtype_coe Bornology.isBounded_image_subtype_val

end Bornology

/-!
### Bounded spaces
-/


open Bornology

instance [BoundedSpace Œ±] [BoundedSpace Œ≤] : BoundedSpace (Œ± √ó Œ≤) := by
  simp [‚Üê cobounded_eq_bot_iff, cobounded_prod]
  -- üéâ no goals

instance [‚àÄ i, BoundedSpace (œÄ i)] : BoundedSpace (‚àÄ i, œÄ i) := by
  simp [‚Üê cobounded_eq_bot_iff, cobounded_pi]
  -- üéâ no goals

theorem boundedSpace_induced_iff {Œ± Œ≤ : Type*} [Bornology Œ≤] {f : Œ± ‚Üí Œ≤} :
    @BoundedSpace Œ± (Bornology.induced f) ‚Üî IsBounded (range f) := by
  rw [‚Üê @isBounded_univ _ (Bornology.induced f), isBounded_induced, image_univ]
  -- üéâ no goals
-- porting note: had to explicitly provided the bornology to `isBounded_univ`.
#align bounded_space_induced_iff boundedSpace_induced_iff

theorem boundedSpace_subtype_iff {p : Œ± ‚Üí Prop} :
    BoundedSpace (Subtype p) ‚Üî IsBounded { x | p x } := by
  rw [boundedSpace_induced_iff, Subtype.range_coe_subtype]
  -- üéâ no goals
#align bounded_space_subtype_iff boundedSpace_subtype_iff

theorem boundedSpace_val_set_iff {s : Set Œ±} : BoundedSpace s ‚Üî IsBounded s :=
  boundedSpace_subtype_iff
#align bounded_space_coe_set_iff boundedSpace_val_set_iff

alias ‚ü®_, Bornology.IsBounded.boundedSpace_subtype‚ü© := boundedSpace_subtype_iff
#align bornology.is_bounded.bounded_space_subtype Bornology.IsBounded.boundedSpace_subtype

alias ‚ü®_, Bornology.IsBounded.boundedSpace_val‚ü© := boundedSpace_val_set_iff
#align bornology.is_bounded.bounded_space_coe Bornology.IsBounded.boundedSpace_val

instance [BoundedSpace Œ±] {p : Œ± ‚Üí Prop} : BoundedSpace (Subtype p) :=
  (IsBounded.all { x | p x }).boundedSpace_subtype

/-!
### `Additive`, `Multiplicative`

The bornology on those type synonyms is inherited without change.
-/


instance : Bornology (Additive Œ±) :=
  ‚ÄπBornology Œ±‚Ä∫

instance : Bornology (Multiplicative Œ±) :=
  ‚ÄπBornology Œ±‚Ä∫

instance [BoundedSpace Œ±] : BoundedSpace (Additive Œ±) :=
  ‚ÄπBoundedSpace Œ±‚Ä∫

instance [BoundedSpace Œ±] : BoundedSpace (Multiplicative Œ±) :=
  ‚ÄπBoundedSpace Œ±‚Ä∫

/-!
### Order dual

The bornology on this type synonym is inherited without change.
-/


instance : Bornology Œ±·µí·µà :=
  ‚ÄπBornology Œ±‚Ä∫

instance [BoundedSpace Œ±] : BoundedSpace Œ±·µí·µà :=
  ‚ÄπBoundedSpace Œ±‚Ä∫
