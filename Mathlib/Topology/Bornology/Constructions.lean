/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Algebra.Group.TypeTags.Basic
import Mathlib.Topology.Bornology.Basic

/-!
# Bornology structure on products and subtypes

In this file we define `Bornology` and `BoundedSpace` instances on `Œ± √ó Œ≤`, `Œ† i, X i`, and
`{x // p x}`. We also prove basic lemmas about `Bornology.cobounded` and `Bornology.IsBounded`
on these types.
-/


open Set Filter Bornology Function

open Filter

variable {Œ± Œ≤ Œπ : Type*} {X : Œπ ‚Üí Type*} [Bornology Œ±] [Bornology Œ≤]
  [‚àÄ i, Bornology (X i)]

instance Prod.instBornology : Bornology (Œ± √ó Œ≤) where
  cobounded := (cobounded Œ±).coprod (cobounded Œ≤)
  le_cofinite :=
    @coprod_cofinite Œ± Œ≤ ‚ñ∏ coprod_mono ‚ÄπBornology Œ±‚Ä∫.le_cofinite ‚ÄπBornology Œ≤‚Ä∫.le_cofinite

instance Pi.instBornology : Bornology (‚àÄ i, X i) where
  cobounded := Filter.coprod·µ¢ fun i => cobounded (X i)
  le_cofinite := iSup_le fun _ ‚Ü¶ (comap_mono (Bornology.le_cofinite _)).trans (comap_cofinite_le _)

/-- Inverse image of a bornology. -/
abbrev Bornology.induced {Œ± Œ≤ : Type*} [Bornology Œ≤] (f : Œ± ‚Üí Œ≤) : Bornology Œ± where
  cobounded := comap f (cobounded Œ≤)
  le_cofinite := (comap_mono (Bornology.le_cofinite Œ≤)).trans (comap_cofinite_le _)

instance {p : Œ± ‚Üí Prop} : Bornology (Subtype p) :=
  Bornology.induced (Subtype.val : Subtype p ‚Üí Œ±)

namespace Bornology

/-!
### Bounded sets in `Œ± √ó Œ≤`
-/


theorem cobounded_prod : cobounded (Œ± √ó Œ≤) = (cobounded Œ±).coprod (cobounded Œ≤) :=
  rfl

theorem isBounded_image_fst_and_snd {s : Set (Œ± √ó Œ≤)} :
    IsBounded (Prod.fst '' s) ‚àß IsBounded (Prod.snd '' s) ‚Üî IsBounded s :=
  compl_mem_coprod.symm

lemma IsBounded.image_fst {s : Set (Œ± √ó Œ≤)} (hs : IsBounded s) : IsBounded (Prod.fst '' s) :=
  (isBounded_image_fst_and_snd.2 hs).1

lemma IsBounded.image_snd {s : Set (Œ± √ó Œ≤)} (hs : IsBounded s) : IsBounded (Prod.snd '' s) :=
  (isBounded_image_fst_and_snd.2 hs).2

variable {s : Set Œ±} {t : Set Œ≤} {S : ‚àÄ i, Set (X i)}

theorem IsBounded.fst_of_prod (h : IsBounded (s √óÀ¢ t)) (ht : t.Nonempty) : IsBounded s :=
  fst_image_prod s ht ‚ñ∏ h.image_fst

theorem IsBounded.snd_of_prod (h : IsBounded (s √óÀ¢ t)) (hs : s.Nonempty) : IsBounded t :=
  snd_image_prod hs t ‚ñ∏ h.image_snd

theorem IsBounded.prod (hs : IsBounded s) (ht : IsBounded t) : IsBounded (s √óÀ¢ t) :=
  isBounded_image_fst_and_snd.1
    ‚ü®hs.subset <| fst_image_prod_subset _ _, ht.subset <| snd_image_prod_subset _ _‚ü©

theorem isBounded_prod_of_nonempty (hne : Set.Nonempty (s √óÀ¢ t)) :
    IsBounded (s √óÀ¢ t) ‚Üî IsBounded s ‚àß IsBounded t :=
  ‚ü®fun h => ‚ü®h.fst_of_prod hne.snd, h.snd_of_prod hne.fst‚ü©, fun h => h.1.prod h.2‚ü©

theorem isBounded_prod : IsBounded (s √óÀ¢ t) ‚Üî s = ‚àÖ ‚à® t = ‚àÖ ‚à® IsBounded s ‚àß IsBounded t := by
  rcases s.eq_empty_or_nonempty with (rfl | hs); ¬∑ simp
  rcases t.eq_empty_or_nonempty with (rfl | ht); ¬∑ simp
  simp only [hs.ne_empty, ht.ne_empty, isBounded_prod_of_nonempty (hs.prod ht), false_or]

theorem isBounded_prod_self : IsBounded (s √óÀ¢ s) ‚Üî IsBounded s := by
  rcases s.eq_empty_or_nonempty with (rfl | hs); ¬∑ simp
  exact (isBounded_prod_of_nonempty (hs.prod hs)).trans and_self_iff

section tendsto

lemma _root_.Filter.Tendsto.cobounded_prod {Œ≥ : Type*} {f : Œ± √ó Œ≤ ‚Üí Œ≥} {l : Filter Œ≥}
    (h‚ÇÅ : ‚àÄ s : Set Œ±, IsBounded s ‚Üí Tendsto f (ùìü s √óÀ¢ cobounded Œ≤) l)
    (h‚ÇÇ : Tendsto f (cobounded Œ± √óÀ¢ ‚ä§) l) :
    Tendsto f (cobounded (Œ± √ó Œ≤)) l := by
  intro s hs
  rw [mem_map, ‚Üê isCobounded_def, ‚Üê isBounded_compl_iff, ‚Üê preimage_compl]
  specialize h‚ÇÇ hs
  rw [mem_map, mem_prod_top] at h‚ÇÇ
  set t := {a | ‚àÄ (b : Œ≤), (a, b) ‚àà f ‚Åª¬π' s}
  have ht : t √óÀ¢ univ ‚äÜ f ‚Åª¬π' s := by grind
  specialize h‚ÇÅ t·∂ú (IsCobounded.compl h‚ÇÇ) hs
  rw [mem_map, mem_prod_iff] at h‚ÇÅ
  obtain ‚ü®t‚ÇÅ, ht‚ÇÅ, t‚ÇÇ, ht‚ÇÇ, H‚ü© := h‚ÇÅ
  rw [mem_principal, compl_subset_comm] at ht‚ÇÅ
  rw [‚Üê compl_subset_compl, ‚Üê preimage_compl, compl_prod_eq_union] at H ht
  simp only [compl_univ, prod_empty, union_empty] at ht
  have := subset_inter H ht
  rw [union_inter_distrib_right, prod_inter_prod, prod_inter_prod] at this
  have ht‚ÇÅt : t‚ÇÅ·∂ú ‚à© t·∂ú = ‚àÖ := (disjoint_compl_left_iff_subset.mpr ht‚ÇÅ).symm.inter_eq
  simp only [ht‚ÇÅt, inter_self, empty_prod, univ_inter, inter_univ, empty_union] at this
  exact IsCobounded.compl h‚ÇÇ |>.prod (IsCobounded.compl ht‚ÇÇ) |>.subset this

end tendsto

/-!
### Bounded sets in `Œ† i, X i`
-/


theorem cobounded_pi : cobounded (‚àÄ i, X i) = Filter.coprod·µ¢ fun i => cobounded (X i) :=
  rfl

theorem forall_isBounded_image_eval_iff {s : Set (‚àÄ i, X i)} :
    (‚àÄ i, IsBounded (eval i '' s)) ‚Üî IsBounded s :=
  compl_mem_coprod·µ¢.symm

lemma IsBounded.image_eval {s : Set (‚àÄ i, X i)} (hs : IsBounded s) (i : Œπ) :
    IsBounded (eval i '' s) :=
  forall_isBounded_image_eval_iff.2 hs i

theorem IsBounded.pi (h : ‚àÄ i, IsBounded (S i)) : IsBounded (pi univ S) :=
  forall_isBounded_image_eval_iff.1 fun i => (h i).subset eval_image_univ_pi_subset

theorem isBounded_pi_of_nonempty (hne : (pi univ S).Nonempty) :
    IsBounded (pi univ S) ‚Üî ‚àÄ i, IsBounded (S i) :=
  ‚ü®fun H i => @eval_image_univ_pi _ _ _ i hne ‚ñ∏ forall_isBounded_image_eval_iff.2 H i, IsBounded.pi‚ü©

theorem isBounded_pi : IsBounded (pi univ S) ‚Üî (‚àÉ i, S i = ‚àÖ) ‚à® ‚àÄ i, IsBounded (S i) := by
  by_cases hne : ‚àÉ i, S i = ‚àÖ
  ¬∑ simp [hne, univ_pi_eq_empty_iff.2 hne]
  ¬∑ simp only [hne, false_or]
    simp only [not_exists, ‚Üê nonempty_iff_ne_empty, ‚Üê univ_pi_nonempty_iff] at hne
    exact isBounded_pi_of_nonempty hne

/-!
### Bounded sets in `{x // p x}`
-/


theorem isBounded_induced {Œ± Œ≤ : Type*} [Bornology Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ±} :
    @IsBounded Œ± (Bornology.induced f) s ‚Üî IsBounded (f '' s) :=
  compl_mem_comap

theorem isBounded_image_subtype_val {p : Œ± ‚Üí Prop} {s : Set { x // p x }} :
    IsBounded (Subtype.val '' s) ‚Üî IsBounded s :=
  isBounded_induced.symm

end Bornology

/-!
### Bounded spaces
-/


open Bornology

instance [BoundedSpace Œ±] [BoundedSpace Œ≤] : BoundedSpace (Œ± √ó Œ≤) := by
  simp [‚Üê cobounded_eq_bot_iff, cobounded_prod]

instance [‚àÄ i, BoundedSpace (X i)] : BoundedSpace (‚àÄ i, X i) := by
  simp [‚Üê cobounded_eq_bot_iff, cobounded_pi]

theorem boundedSpace_induced_iff {Œ± Œ≤ : Type*} [Bornology Œ≤] {f : Œ± ‚Üí Œ≤} :
    @BoundedSpace Œ± (Bornology.induced f) ‚Üî IsBounded (range f) := by
  rw [‚Üê @isBounded_univ, isBounded_induced, image_univ]

theorem boundedSpace_subtype_iff {p : Œ± ‚Üí Prop} :
    BoundedSpace (Subtype p) ‚Üî IsBounded { x | p x } := by
  rw [boundedSpace_induced_iff, Subtype.range_coe_subtype]

theorem boundedSpace_val_set_iff {s : Set Œ±} : BoundedSpace s ‚Üî IsBounded s :=
  boundedSpace_subtype_iff

alias ‚ü®_, Bornology.IsBounded.boundedSpace_subtype‚ü© := boundedSpace_subtype_iff

alias ‚ü®_, Bornology.IsBounded.boundedSpace_val‚ü© := boundedSpace_val_set_iff

instance [BoundedSpace Œ±] {p : Œ± ‚Üí Prop} : BoundedSpace (Subtype p) :=
  (IsBounded.all { x | p x }).boundedSpace_subtype

/-!
### `Additive`, `Multiplicative`

The bornology on those type synonyms is inherited without change.
-/


instance : Bornology (Additive Œ±) :=
  ‚ÄπBornology Œ±‚Ä∫

instance : Bornology (Multiplicative Œ±) :=
  ‚ÄπBornology Œ±‚Ä∫

instance [BoundedSpace Œ±] : BoundedSpace (Additive Œ±) :=
  ‚ÄπBoundedSpace Œ±‚Ä∫

instance [BoundedSpace Œ±] : BoundedSpace (Multiplicative Œ±) :=
  ‚ÄπBoundedSpace Œ±‚Ä∫

/-!
### Order dual

The bornology on this type synonym is inherited without change.
-/


instance : Bornology Œ±·µí·µà :=
  ‚ÄπBornology Œ±‚Ä∫

instance [BoundedSpace Œ±] : BoundedSpace Œ±·µí·µà :=
  ‚ÄπBoundedSpace Œ±‚Ä∫
