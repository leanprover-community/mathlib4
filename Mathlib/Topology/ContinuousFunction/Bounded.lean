/-
Copyright (c) 2018 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Mario Carneiro, Yury Kudryashov, Heather Macbeth
-/
import Mathlib.Algebra.Module.MinimalAxioms
import Mathlib.Topology.ContinuousFunction.Algebra
import Mathlib.Analysis.Normed.Order.Lattice
import Mathlib.Analysis.NormedSpace.OperatorNorm.Basic
import Mathlib.Analysis.NormedSpace.Star.Basic
import Mathlib.Analysis.NormedSpace.ContinuousLinearMap
import Mathlib.Topology.Bornology.BoundedOperation

#align_import topology.continuous_function.bounded from "leanprover-community/mathlib"@"5dc275ec639221ca4d5f56938eb966f6ad9bc89f"

/-!
# Bounded continuous functions

The type of bounded continuous functions taking values in a metric space, with
the uniform distance.

-/


noncomputable section

open scoped Classical
open Topology Bornology NNReal uniformity UniformConvergence

open Set Filter Metric Function

universe u v w

variable {F : Type*} {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w}

/-- `Œ± ‚Üí·µá Œ≤` is the type of bounded continuous functions `Œ± ‚Üí Œ≤` from a topological space to a
metric space.

When possible, instead of parametrizing results over `(f : Œ± ‚Üí·µá Œ≤)`,
you should parametrize over `(F : Type*) [BoundedContinuousMapClass F Œ± Œ≤] (f : F)`.

When you extend this structure, make sure to extend `BoundedContinuousMapClass`. -/
structure BoundedContinuousFunction (Œ± : Type u) (Œ≤ : Type v) [TopologicalSpace Œ±]
    [PseudoMetricSpace Œ≤] extends ContinuousMap Œ± Œ≤ : Type max u v where
  map_bounded' : ‚àÉ C, ‚àÄ x y, dist (toFun x) (toFun y) ‚â§ C
#align bounded_continuous_function BoundedContinuousFunction

scoped[BoundedContinuousFunction] infixr:25 " ‚Üí·µá " => BoundedContinuousFunction

section

-- Porting note: Changed type of `Œ± Œ≤` from `Type*` to `outParam Type*`.
/-- `BoundedContinuousMapClass F Œ± Œ≤` states that `F` is a type of bounded continuous maps.

You should also extend this typeclass when you extend `BoundedContinuousFunction`. -/
class BoundedContinuousMapClass (F : Type*) (Œ± Œ≤ : outParam Type*) [TopologicalSpace Œ±]
    [PseudoMetricSpace Œ≤] [FunLike F Œ± Œ≤] extends ContinuousMapClass F Œ± Œ≤ : Prop where
  map_bounded (f : F) : ‚àÉ C, ‚àÄ x y, dist (f x) (f y) ‚â§ C
#align bounded_continuous_map_class BoundedContinuousMapClass

end

export BoundedContinuousMapClass (map_bounded)

namespace BoundedContinuousFunction

section Basics

variable [TopologicalSpace Œ±] [PseudoMetricSpace Œ≤] [PseudoMetricSpace Œ≥]
variable {f g : Œ± ‚Üí·µá Œ≤} {x : Œ±} {C : ‚Ñù}

instance instFunLike : FunLike (Œ± ‚Üí·µá Œ≤) Œ± Œ≤ where
  coe f := f.toFun
  coe_injective' f g h := by
    obtain ‚ü®‚ü®_, _‚ü©, _‚ü© := f
    obtain ‚ü®‚ü®_, _‚ü©, _‚ü© := g
    congr

instance instBoundedContinuousMapClass : BoundedContinuousMapClass (Œ± ‚Üí·µá Œ≤) Œ± Œ≤ where
  map_continuous f := f.continuous_toFun
  map_bounded f := f.map_bounded'

instance instCoeTC [FunLike F Œ± Œ≤] [BoundedContinuousMapClass F Œ± Œ≤] : CoeTC F (Œ± ‚Üí·µá Œ≤) :=
  ‚ü®fun f =>
    { toFun := f
      continuous_toFun := map_continuous f
      map_bounded' := map_bounded f }‚ü©

@[simp]
theorem coe_to_continuous_fun (f : Œ± ‚Üí·µá Œ≤) : (f.toContinuousMap : Œ± ‚Üí Œ≤) = f := rfl
#align bounded_continuous_function.coe_to_continuous_fun BoundedContinuousFunction.coe_to_continuous_fun

/-- See Note [custom simps projection]. We need to specify this projection explicitly in this case,
  because it is a composition of multiple projections. -/
def Simps.apply (h : Œ± ‚Üí·µá Œ≤) : Œ± ‚Üí Œ≤ := h
#align bounded_continuous_function.simps.apply BoundedContinuousFunction.Simps.apply

initialize_simps_projections BoundedContinuousFunction (toContinuousMap_toFun ‚Üí apply)

protected theorem bounded (f : Œ± ‚Üí·µá Œ≤) : ‚àÉ C, ‚àÄ x y : Œ±, dist (f x) (f y) ‚â§ C :=
  f.map_bounded'
#align bounded_continuous_function.bounded BoundedContinuousFunction.bounded

protected theorem continuous (f : Œ± ‚Üí·µá Œ≤) : Continuous f :=
  f.toContinuousMap.continuous
#align bounded_continuous_function.continuous BoundedContinuousFunction.continuous

@[ext]
theorem ext (h : ‚àÄ x, f x = g x) : f = g :=
  DFunLike.ext _ _ h
#align bounded_continuous_function.ext BoundedContinuousFunction.ext

theorem isBounded_range (f : Œ± ‚Üí·µá Œ≤) : IsBounded (range f) :=
  isBounded_range_iff.2 f.bounded
#align bounded_continuous_function.bounded_range BoundedContinuousFunction.isBounded_range

theorem isBounded_image (f : Œ± ‚Üí·µá Œ≤) (s : Set Œ±) : IsBounded (f '' s) :=
  f.isBounded_range.subset <| image_subset_range _ _
#align bounded_continuous_function.bounded_image BoundedContinuousFunction.isBounded_image

theorem eq_of_empty [h : IsEmpty Œ±] (f g : Œ± ‚Üí·µá Œ≤) : f = g :=
  ext <| h.elim
#align bounded_continuous_function.eq_of_empty BoundedContinuousFunction.eq_of_empty

/-- A continuous function with an explicit bound is a bounded continuous function. -/
def mkOfBound (f : C(Œ±, Œ≤)) (C : ‚Ñù) (h : ‚àÄ x y : Œ±, dist (f x) (f y) ‚â§ C) : Œ± ‚Üí·µá Œ≤ :=
  ‚ü®f, ‚ü®C, h‚ü©‚ü©
#align bounded_continuous_function.mk_of_bound BoundedContinuousFunction.mkOfBound

@[simp]
theorem mkOfBound_coe {f} {C} {h} : (mkOfBound f C h : Œ± ‚Üí Œ≤) = (f : Œ± ‚Üí Œ≤) := rfl
#align bounded_continuous_function.mk_of_bound_coe BoundedContinuousFunction.mkOfBound_coe

/-- A continuous function on a compact space is automatically a bounded continuous function. -/
def mkOfCompact [CompactSpace Œ±] (f : C(Œ±, Œ≤)) : Œ± ‚Üí·µá Œ≤ :=
  ‚ü®f, isBounded_range_iff.1 (isCompact_range f.continuous).isBounded‚ü©
#align bounded_continuous_function.mk_of_compact BoundedContinuousFunction.mkOfCompact

@[simp]
theorem mkOfCompact_apply [CompactSpace Œ±] (f : C(Œ±, Œ≤)) (a : Œ±) : mkOfCompact f a = f a := rfl
#align bounded_continuous_function.mk_of_compact_apply BoundedContinuousFunction.mkOfCompact_apply

/-- If a function is bounded on a discrete space, it is automatically continuous,
and therefore gives rise to an element of the type of bounded continuous functions. -/
@[simps]
def mkOfDiscrete [DiscreteTopology Œ±] (f : Œ± ‚Üí Œ≤) (C : ‚Ñù) (h : ‚àÄ x y : Œ±, dist (f x) (f y) ‚â§ C) :
    Œ± ‚Üí·µá Œ≤ :=
  ‚ü®‚ü®f, continuous_of_discreteTopology‚ü©, ‚ü®C, h‚ü©‚ü©
#align bounded_continuous_function.mk_of_discrete BoundedContinuousFunction.mkOfDiscrete

/-- The uniform distance between two bounded continuous functions. -/
instance instDist : Dist (Œ± ‚Üí·µá Œ≤) :=
  ‚ü®fun f g => sInf { C | 0 ‚â§ C ‚àß ‚àÄ x : Œ±, dist (f x) (g x) ‚â§ C }‚ü©

theorem dist_eq : dist f g = sInf { C | 0 ‚â§ C ‚àß ‚àÄ x : Œ±, dist (f x) (g x) ‚â§ C } := rfl
#align bounded_continuous_function.dist_eq BoundedContinuousFunction.dist_eq

theorem dist_set_exists : ‚àÉ C, 0 ‚â§ C ‚àß ‚àÄ x : Œ±, dist (f x) (g x) ‚â§ C := by
  rcases isBounded_iff.1 (f.isBounded_range.union g.isBounded_range) with ‚ü®C, hC‚ü©
  refine ‚ü®max 0 C, le_max_left _ _, fun x => (hC ?_ ?_).trans (le_max_right _ _)‚ü©
    <;> [left; right]
    <;> apply mem_range_self
#align bounded_continuous_function.dist_set_exists BoundedContinuousFunction.dist_set_exists

/-- The pointwise distance is controlled by the distance between functions, by definition. -/
theorem dist_coe_le_dist (x : Œ±) : dist (f x) (g x) ‚â§ dist f g :=
  le_csInf dist_set_exists fun _ hb => hb.2 x
#align bounded_continuous_function.dist_coe_le_dist BoundedContinuousFunction.dist_coe_le_dist

/- This lemma will be needed in the proof of the metric space instance, but it will become
useless afterwards as it will be superseded by the general result that the distance is nonnegative
in metric spaces. -/
private theorem dist_nonneg' : 0 ‚â§ dist f g :=
  le_csInf dist_set_exists fun _ => And.left

/-- The distance between two functions is controlled by the supremum of the pointwise distances. -/
theorem dist_le (C0 : (0 : ‚Ñù) ‚â§ C) : dist f g ‚â§ C ‚Üî ‚àÄ x : Œ±, dist (f x) (g x) ‚â§ C :=
  ‚ü®fun h x => le_trans (dist_coe_le_dist x) h, fun H => csInf_le ‚ü®0, fun _ => And.left‚ü© ‚ü®C0, H‚ü©‚ü©
#align bounded_continuous_function.dist_le BoundedContinuousFunction.dist_le

theorem dist_le_iff_of_nonempty [Nonempty Œ±] : dist f g ‚â§ C ‚Üî ‚àÄ x, dist (f x) (g x) ‚â§ C :=
  ‚ü®fun h x => le_trans (dist_coe_le_dist x) h,
    fun w => (dist_le (le_trans dist_nonneg (w (Nonempty.some ‚Äπ_‚Ä∫)))).mpr w‚ü©
#align bounded_continuous_function.dist_le_iff_of_nonempty BoundedContinuousFunction.dist_le_iff_of_nonempty

theorem dist_lt_of_nonempty_compact [Nonempty Œ±] [CompactSpace Œ±]
    (w : ‚àÄ x : Œ±, dist (f x) (g x) < C) : dist f g < C := by
  have c : Continuous fun x => dist (f x) (g x) := by continuity
  obtain ‚ü®x, -, le‚ü© :=
    IsCompact.exists_isMaxOn isCompact_univ Set.univ_nonempty (Continuous.continuousOn c)
  exact lt_of_le_of_lt (dist_le_iff_of_nonempty.mpr fun y => le trivial) (w x)
#align bounded_continuous_function.dist_lt_of_nonempty_compact BoundedContinuousFunction.dist_lt_of_nonempty_compact

theorem dist_lt_iff_of_compact [CompactSpace Œ±] (C0 : (0 : ‚Ñù) < C) :
    dist f g < C ‚Üî ‚àÄ x : Œ±, dist (f x) (g x) < C := by
  fconstructor
  ¬∑ intro w x
    exact lt_of_le_of_lt (dist_coe_le_dist x) w
  ¬∑ by_cases h : Nonempty Œ±
    ¬∑ exact dist_lt_of_nonempty_compact
    ¬∑ rintro -
      convert C0
      apply le_antisymm _ dist_nonneg'
      rw [dist_eq]
      exact csInf_le ‚ü®0, fun C => And.left‚ü© ‚ü®le_rfl, fun x => False.elim (h (Nonempty.intro x))‚ü©
#align bounded_continuous_function.dist_lt_iff_of_compact BoundedContinuousFunction.dist_lt_iff_of_compact

theorem dist_lt_iff_of_nonempty_compact [Nonempty Œ±] [CompactSpace Œ±] :
    dist f g < C ‚Üî ‚àÄ x : Œ±, dist (f x) (g x) < C :=
  ‚ü®fun w x => lt_of_le_of_lt (dist_coe_le_dist x) w, dist_lt_of_nonempty_compact‚ü©
#align bounded_continuous_function.dist_lt_iff_of_nonempty_compact BoundedContinuousFunction.dist_lt_iff_of_nonempty_compact

/-- The type of bounded continuous functions, with the uniform distance, is a pseudometric space. -/
instance instPseudoMetricSpace : PseudoMetricSpace (Œ± ‚Üí·µá Œ≤) where
  dist_self f := le_antisymm ((dist_le le_rfl).2 fun x => by simp) dist_nonneg'
  dist_comm f g := by simp [dist_eq, dist_comm]
  dist_triangle f g h := (dist_le (add_nonneg dist_nonneg' dist_nonneg')).2
    fun x => le_trans (dist_triangle _ _ _) (add_le_add (dist_coe_le_dist _) (dist_coe_le_dist _))
  -- Porting note (#10888): added proof for `edist_dist`
  edist_dist x y := by dsimp; congr; simp [dist_nonneg']

/-- The type of bounded continuous functions, with the uniform distance, is a metric space. -/
instance instMetricSpace {Œ≤} [MetricSpace Œ≤] : MetricSpace (Œ± ‚Üí·µá Œ≤) where
  eq_of_dist_eq_zero hfg := by
    ext x
    exact eq_of_dist_eq_zero (le_antisymm (hfg ‚ñ∏ dist_coe_le_dist _) dist_nonneg)

theorem nndist_eq : nndist f g = sInf { C | ‚àÄ x : Œ±, nndist (f x) (g x) ‚â§ C } :=
  Subtype.ext <| dist_eq.trans <| by
    rw [val_eq_coe, coe_sInf, coe_image]
    simp_rw [mem_setOf_eq, ‚Üê NNReal.coe_le_coe, coe_mk, exists_prop, coe_nndist]
#align bounded_continuous_function.nndist_eq BoundedContinuousFunction.nndist_eq

theorem nndist_set_exists : ‚àÉ C, ‚àÄ x : Œ±, nndist (f x) (g x) ‚â§ C :=
  Subtype.exists.mpr <| dist_set_exists.imp fun _ ‚ü®ha, h‚ü© => ‚ü®ha, h‚ü©
#align bounded_continuous_function.nndist_set_exists BoundedContinuousFunction.nndist_set_exists

theorem nndist_coe_le_nndist (x : Œ±) : nndist (f x) (g x) ‚â§ nndist f g :=
  dist_coe_le_dist x
#align bounded_continuous_function.nndist_coe_le_nndist BoundedContinuousFunction.nndist_coe_le_nndist

/-- On an empty space, bounded continuous functions are at distance 0. -/
theorem dist_zero_of_empty [IsEmpty Œ±] : dist f g = 0 := by
  rw [(ext isEmptyElim : f = g), dist_self]
#align bounded_continuous_function.dist_zero_of_empty BoundedContinuousFunction.dist_zero_of_empty

theorem dist_eq_iSup : dist f g = ‚®Ü x : Œ±, dist (f x) (g x) := by
  cases isEmpty_or_nonempty Œ±
  ¬∑ rw [iSup_of_empty', Real.sSup_empty, dist_zero_of_empty]
  refine (dist_le_iff_of_nonempty.mpr <| le_ciSup ?_).antisymm (ciSup_le dist_coe_le_dist)
  exact dist_set_exists.imp fun C hC => forall_mem_range.2 hC.2
#align bounded_continuous_function.dist_eq_supr BoundedContinuousFunction.dist_eq_iSup

theorem nndist_eq_iSup : nndist f g = ‚®Ü x : Œ±, nndist (f x) (g x) :=
  Subtype.ext <| dist_eq_iSup.trans <| by simp_rw [val_eq_coe, coe_iSup, coe_nndist]
#align bounded_continuous_function.nndist_eq_supr BoundedContinuousFunction.nndist_eq_iSup

theorem tendsto_iff_tendstoUniformly {Œπ : Type*} {F : Œπ ‚Üí Œ± ‚Üí·µá Œ≤} {f : Œ± ‚Üí·µá Œ≤} {l : Filter Œπ} :
    Tendsto F l (ùìù f) ‚Üî TendstoUniformly (fun i => F i) f l :=
  Iff.intro
    (fun h =>
      tendstoUniformly_iff.2 fun Œµ Œµ0 =>
        (Metric.tendsto_nhds.mp h Œµ Œµ0).mp
          (eventually_of_forall fun n hn x =>
            lt_of_le_of_lt (dist_coe_le_dist x) (dist_comm (F n) f ‚ñ∏ hn)))
    fun h =>
    Metric.tendsto_nhds.mpr fun _ Œµ_pos =>
      (h _ (dist_mem_uniformity <| half_pos Œµ_pos)).mp
        (eventually_of_forall fun n hn =>
          lt_of_le_of_lt
            ((dist_le (half_pos Œµ_pos).le).mpr fun x => dist_comm (f x) (F n x) ‚ñ∏ le_of_lt (hn x))
            (half_lt_self Œµ_pos))
#align bounded_continuous_function.tendsto_iff_tendsto_uniformly BoundedContinuousFunction.tendsto_iff_tendstoUniformly

/-- The topology on `Œ± ‚Üí·µá Œ≤` is exactly the topology induced by the natural map to `Œ± ‚Üí·µ§ Œ≤`. -/
theorem inducing_coeFn : Inducing (UniformFun.ofFun ‚àò (‚áë) : (Œ± ‚Üí·µá Œ≤) ‚Üí Œ± ‚Üí·µ§ Œ≤) := by
  rw [inducing_iff_nhds]
  refine fun f => eq_of_forall_le_iff fun l => ?_
  rw [‚Üê tendsto_iff_comap, ‚Üê tendsto_id', tendsto_iff_tendstoUniformly,
    UniformFun.tendsto_iff_tendstoUniformly]
  simp [comp_def]
#align bounded_continuous_function.inducing_coe_fn BoundedContinuousFunction.inducing_coeFn

-- TODO: upgrade to a `UniformEmbedding`
theorem embedding_coeFn : Embedding (UniformFun.ofFun ‚àò (‚áë) : (Œ± ‚Üí·µá Œ≤) ‚Üí Œ± ‚Üí·µ§ Œ≤) :=
  ‚ü®inducing_coeFn, fun _ _ h => ext fun x => congr_fun h x‚ü©
#align bounded_continuous_function.embedding_coe_fn BoundedContinuousFunction.embedding_coeFn

variable (Œ±)

/-- Constant as a continuous bounded function. -/
@[simps! (config := .asFn)] -- Porting note: Changed `simps` to `simps!`
def const (b : Œ≤) : Œ± ‚Üí·µá Œ≤ :=
  ‚ü®ContinuousMap.const Œ± b, 0, by simp‚ü©
#align bounded_continuous_function.const BoundedContinuousFunction.const

variable {Œ±}

theorem const_apply' (a : Œ±) (b : Œ≤) : (const Œ± b : Œ± ‚Üí Œ≤) a = b := rfl
#align bounded_continuous_function.const_apply' BoundedContinuousFunction.const_apply'

/-- If the target space is inhabited, so is the space of bounded continuous functions. -/
instance [Inhabited Œ≤] : Inhabited (Œ± ‚Üí·µá Œ≤) :=
  ‚ü®const Œ± default‚ü©

theorem lipschitz_evalx (x : Œ±) : LipschitzWith 1 fun f : Œ± ‚Üí·µá Œ≤ => f x :=
  LipschitzWith.mk_one fun _ _ => dist_coe_le_dist x
#align bounded_continuous_function.lipschitz_evalx BoundedContinuousFunction.lipschitz_evalx

theorem uniformContinuous_coe : @UniformContinuous (Œ± ‚Üí·µá Œ≤) (Œ± ‚Üí Œ≤) _ _ (‚áë) :=
  uniformContinuous_pi.2 fun x => (lipschitz_evalx x).uniformContinuous
#align bounded_continuous_function.uniform_continuous_coe BoundedContinuousFunction.uniformContinuous_coe

theorem continuous_coe : Continuous fun (f : Œ± ‚Üí·µá Œ≤) x => f x :=
  UniformContinuous.continuous uniformContinuous_coe
#align bounded_continuous_function.continuous_coe BoundedContinuousFunction.continuous_coe

/-- When `x` is fixed, `(f : Œ± ‚Üí·µá Œ≤) ‚Ü¶ f x` is continuous. -/
@[continuity]
theorem continuous_eval_const {x : Œ±} : Continuous fun f : Œ± ‚Üí·µá Œ≤ => f x :=
  (continuous_apply x).comp continuous_coe
#align bounded_continuous_function.continuous_eval_const BoundedContinuousFunction.continuous_eval_const

/-- The evaluation map is continuous, as a joint function of `u` and `x`. -/
@[continuity]
theorem continuous_eval : Continuous fun p : (Œ± ‚Üí·µá Œ≤) √ó Œ± => p.1 p.2 :=
  (continuous_prod_of_continuous_lipschitzWith _ 1 fun f => f.continuous) <| lipschitz_evalx
#align bounded_continuous_function.continuous_eval BoundedContinuousFunction.continuous_eval

/-- Bounded continuous functions taking values in a complete space form a complete space. -/
instance instCompleteSpace [CompleteSpace Œ≤] : CompleteSpace (Œ± ‚Üí·µá Œ≤) :=
  complete_of_cauchySeq_tendsto fun (f : ‚Ñï ‚Üí Œ± ‚Üí·µá Œ≤) (hf : CauchySeq f) => by
    /- We have to show that `f n` converges to a bounded continuous function.
      For this, we prove pointwise convergence to define the limit, then check
      it is a continuous bounded function, and then check the norm convergence. -/
    rcases cauchySeq_iff_le_tendsto_0.1 hf with ‚ü®b, b0, b_bound, b_lim‚ü©
    have f_bdd := fun x n m N hn hm => le_trans (dist_coe_le_dist x) (b_bound n m N hn hm)
    have fx_cau : ‚àÄ x, CauchySeq fun n => f n x :=
      fun x => cauchySeq_iff_le_tendsto_0.2 ‚ü®b, b0, f_bdd x, b_lim‚ü©
    choose F hF using fun x => cauchySeq_tendsto_of_complete (fx_cau x)
    /- `F : Œ± ‚Üí Œ≤`, `hF : ‚àÄ (x : Œ±), Tendsto (fun n ‚Ü¶ ‚Üë(f n) x) atTop (ùìù (F x))`
      `F` is the desired limit function. Check that it is uniformly approximated by `f N`. -/
    have fF_bdd : ‚àÄ x N, dist (f N x) (F x) ‚â§ b N :=
      fun x N => le_of_tendsto (tendsto_const_nhds.dist (hF x))
        (Filter.eventually_atTop.2 ‚ü®N, fun n hn => f_bdd x N n N (le_refl N) hn‚ü©)
    refine ‚ü®‚ü®‚ü®F, ?_‚ü©, ?_‚ü©, ?_‚ü©
    ¬∑ -- Check that `F` is continuous, as a uniform limit of continuous functions
      have : TendstoUniformly (fun n x => f n x) F atTop := by
        refine Metric.tendstoUniformly_iff.2 fun Œµ Œµ0 => ?_
        refine ((tendsto_order.1 b_lim).2 Œµ Œµ0).mono fun n hn x => ?_
        rw [dist_comm]
        exact lt_of_le_of_lt (fF_bdd x n) hn
      exact this.continuous (eventually_of_forall fun N => (f N).continuous)
    ¬∑ -- Check that `F` is bounded
      rcases (f 0).bounded with ‚ü®C, hC‚ü©
      refine ‚ü®C + (b 0 + b 0), fun x y => ?_‚ü©
      calc
        dist (F x) (F y) ‚â§ dist (f 0 x) (f 0 y) + (dist (f 0 x) (F x) + dist (f 0 y) (F y)) :=
          dist_triangle4_left _ _ _ _
        _ ‚â§ C + (b 0 + b 0) := by mono
    ¬∑ -- Check that `F` is close to `f N` in distance terms
      refine tendsto_iff_dist_tendsto_zero.2 (squeeze_zero (fun _ => dist_nonneg) ?_ b_lim)
      exact fun N => (dist_le (b0 _)).2 fun x => fF_bdd x N

/-- Composition of a bounded continuous function and a continuous function. -/
def compContinuous {Œ¥ : Type*} [TopologicalSpace Œ¥] (f : Œ± ‚Üí·µá Œ≤) (g : C(Œ¥, Œ±)) : Œ¥ ‚Üí·µá Œ≤ where
  toContinuousMap := f.1.comp g
  map_bounded' := f.map_bounded'.imp fun _ hC _ _ => hC _ _
#align bounded_continuous_function.comp_continuous BoundedContinuousFunction.compContinuous

@[simp]
theorem coe_compContinuous {Œ¥ : Type*} [TopologicalSpace Œ¥] (f : Œ± ‚Üí·µá Œ≤) (g : C(Œ¥, Œ±)) :
    ‚áë(f.compContinuous g) = f ‚àò g := rfl
#align bounded_continuous_function.coe_comp_continuous BoundedContinuousFunction.coe_compContinuous

@[simp]
theorem compContinuous_apply {Œ¥ : Type*} [TopologicalSpace Œ¥] (f : Œ± ‚Üí·µá Œ≤) (g : C(Œ¥, Œ±)) (x : Œ¥) :
    f.compContinuous g x = f (g x) := rfl
#align bounded_continuous_function.comp_continuous_apply BoundedContinuousFunction.compContinuous_apply

theorem lipschitz_compContinuous {Œ¥ : Type*} [TopologicalSpace Œ¥] (g : C(Œ¥, Œ±)) :
    LipschitzWith 1 fun f : Œ± ‚Üí·µá Œ≤ => f.compContinuous g :=
  LipschitzWith.mk_one fun _ _ => (dist_le dist_nonneg).2 fun x => dist_coe_le_dist (g x)
#align bounded_continuous_function.lipschitz_comp_continuous BoundedContinuousFunction.lipschitz_compContinuous

theorem continuous_compContinuous {Œ¥ : Type*} [TopologicalSpace Œ¥] (g : C(Œ¥, Œ±)) :
    Continuous fun f : Œ± ‚Üí·µá Œ≤ => f.compContinuous g :=
  (lipschitz_compContinuous g).continuous
#align bounded_continuous_function.continuous_comp_continuous BoundedContinuousFunction.continuous_compContinuous

/-- Restrict a bounded continuous function to a set. -/
def restrict (f : Œ± ‚Üí·µá Œ≤) (s : Set Œ±) : s ‚Üí·µá Œ≤ :=
  f.compContinuous <| (ContinuousMap.id _).restrict s
#align bounded_continuous_function.restrict BoundedContinuousFunction.restrict

@[simp]
theorem coe_restrict (f : Œ± ‚Üí·µá Œ≤) (s : Set Œ±) : ‚áë(f.restrict s) = f ‚àò (‚Üë) := rfl
#align bounded_continuous_function.coe_restrict BoundedContinuousFunction.coe_restrict

@[simp]
theorem restrict_apply (f : Œ± ‚Üí·µá Œ≤) (s : Set Œ±) (x : s) : f.restrict s x = f x := rfl
#align bounded_continuous_function.restrict_apply BoundedContinuousFunction.restrict_apply

/-- Composition (in the target) of a bounded continuous function with a Lipschitz map again
gives a bounded continuous function. -/
def comp (G : Œ≤ ‚Üí Œ≥) {C : ‚Ñù‚â•0} (H : LipschitzWith C G) (f : Œ± ‚Üí·µá Œ≤) : Œ± ‚Üí·µá Œ≥ :=
  ‚ü®‚ü®fun x => G (f x), H.continuous.comp f.continuous‚ü©,
    let ‚ü®D, hD‚ü© := f.bounded
    ‚ü®max C 0 * D, fun x y =>
      calc
        dist (G (f x)) (G (f y)) ‚â§ C * dist (f x) (f y) := H.dist_le_mul _ _
        _ ‚â§ max C 0 * dist (f x) (f y) := by gcongr; apply le_max_left
        _ ‚â§ max C 0 * D := by gcongr; apply hD
        ‚ü©‚ü©
#align bounded_continuous_function.comp BoundedContinuousFunction.comp

/-- The composition operator (in the target) with a Lipschitz map is Lipschitz. -/
theorem lipschitz_comp {G : Œ≤ ‚Üí Œ≥} {C : ‚Ñù‚â•0} (H : LipschitzWith C G) :
    LipschitzWith C (comp G H : (Œ± ‚Üí·µá Œ≤) ‚Üí Œ± ‚Üí·µá Œ≥) :=
  LipschitzWith.of_dist_le_mul fun f g =>
    (dist_le (mul_nonneg C.2 dist_nonneg)).2 fun x =>
      calc
        dist (G (f x)) (G (g x)) ‚â§ C * dist (f x) (g x) := H.dist_le_mul _ _
        _ ‚â§ C * dist f g := by gcongr; apply dist_coe_le_dist
#align bounded_continuous_function.lipschitz_comp BoundedContinuousFunction.lipschitz_comp

/-- The composition operator (in the target) with a Lipschitz map is uniformly continuous. -/
theorem uniformContinuous_comp {G : Œ≤ ‚Üí Œ≥} {C : ‚Ñù‚â•0} (H : LipschitzWith C G) :
    UniformContinuous (comp G H : (Œ± ‚Üí·µá Œ≤) ‚Üí Œ± ‚Üí·µá Œ≥) :=
  (lipschitz_comp H).uniformContinuous
#align bounded_continuous_function.uniform_continuous_comp BoundedContinuousFunction.uniformContinuous_comp

/-- The composition operator (in the target) with a Lipschitz map is continuous. -/
theorem continuous_comp {G : Œ≤ ‚Üí Œ≥} {C : ‚Ñù‚â•0} (H : LipschitzWith C G) :
    Continuous (comp G H : (Œ± ‚Üí·µá Œ≤) ‚Üí Œ± ‚Üí·µá Œ≥) :=
  (lipschitz_comp H).continuous
#align bounded_continuous_function.continuous_comp BoundedContinuousFunction.continuous_comp

/-- Restriction (in the target) of a bounded continuous function taking values in a subset. -/
def codRestrict (s : Set Œ≤) (f : Œ± ‚Üí·µá Œ≤) (H : ‚àÄ x, f x ‚àà s) : Œ± ‚Üí·µá s :=
  ‚ü®‚ü®s.codRestrict f H, f.continuous.subtype_mk _‚ü©, f.bounded‚ü©
#align bounded_continuous_function.cod_restrict BoundedContinuousFunction.codRestrict

section Extend

variable {Œ¥ : Type*} [TopologicalSpace Œ¥] [DiscreteTopology Œ¥]

/-- A version of `Function.extend` for bounded continuous maps. We assume that the domain has
discrete topology, so we only need to verify boundedness. -/
nonrec def extend (f : Œ± ‚Ü™ Œ¥) (g : Œ± ‚Üí·µá Œ≤) (h : Œ¥ ‚Üí·µá Œ≤) : Œ¥ ‚Üí·µá Œ≤ where
  toFun := extend f g h
  continuous_toFun := continuous_of_discreteTopology
  map_bounded' := by
    rw [‚Üê isBounded_range_iff, range_extend f.injective]
    exact g.isBounded_range.union (h.isBounded_image _)
#align bounded_continuous_function.extend BoundedContinuousFunction.extend

@[simp]
theorem extend_apply (f : Œ± ‚Ü™ Œ¥) (g : Œ± ‚Üí·µá Œ≤) (h : Œ¥ ‚Üí·µá Œ≤) (x : Œ±) : extend f g h (f x) = g x :=
  f.injective.extend_apply _ _ _
#align bounded_continuous_function.extend_apply BoundedContinuousFunction.extend_apply

@[simp]
nonrec theorem extend_comp (f : Œ± ‚Ü™ Œ¥) (g : Œ± ‚Üí·µá Œ≤) (h : Œ¥ ‚Üí·µá Œ≤) : extend f g h ‚àò f = g :=
  extend_comp f.injective _ _
#align bounded_continuous_function.extend_comp BoundedContinuousFunction.extend_comp

nonrec theorem extend_apply' {f : Œ± ‚Ü™ Œ¥} {x : Œ¥} (hx : x ‚àâ range f) (g : Œ± ‚Üí·µá Œ≤) (h : Œ¥ ‚Üí·µá Œ≤) :
    extend f g h x = h x :=
  extend_apply' _ _ _ hx
#align bounded_continuous_function.extend_apply' BoundedContinuousFunction.extend_apply'

theorem extend_of_empty [IsEmpty Œ±] (f : Œ± ‚Ü™ Œ¥) (g : Œ± ‚Üí·µá Œ≤) (h : Œ¥ ‚Üí·µá Œ≤) : extend f g h = h :=
  DFunLike.coe_injective <| Function.extend_of_isEmpty f g h
#align bounded_continuous_function.extend_of_empty BoundedContinuousFunction.extend_of_empty

@[simp]
theorem dist_extend_extend (f : Œ± ‚Ü™ Œ¥) (g‚ÇÅ g‚ÇÇ : Œ± ‚Üí·µá Œ≤) (h‚ÇÅ h‚ÇÇ : Œ¥ ‚Üí·µá Œ≤) :
    dist (g‚ÇÅ.extend f h‚ÇÅ) (g‚ÇÇ.extend f h‚ÇÇ) =
      max (dist g‚ÇÅ g‚ÇÇ) (dist (h‚ÇÅ.restrict (range f)·∂ú) (h‚ÇÇ.restrict (range f)·∂ú)) := by
  refine le_antisymm ((dist_le <| le_max_iff.2 <| Or.inl dist_nonneg).2 fun x => ?_) (max_le ?_ ?_)
  ¬∑ rcases em (‚àÉ y, f y = x) with (‚ü®x, rfl‚ü© | hx)
    ¬∑ simp only [extend_apply]
      exact (dist_coe_le_dist x).trans (le_max_left _ _)
    ¬∑ simp only [extend_apply' hx]
      lift x to ((range f)·∂ú : Set Œ¥) using hx
      calc
        dist (h‚ÇÅ x) (h‚ÇÇ x) = dist (h‚ÇÅ.restrict (range f)·∂ú x) (h‚ÇÇ.restrict (range f)·∂ú x) := rfl
        _ ‚â§ dist (h‚ÇÅ.restrict (range f)·∂ú) (h‚ÇÇ.restrict (range f)·∂ú) := dist_coe_le_dist x
        _ ‚â§ _ := le_max_right _ _
  ¬∑ refine (dist_le dist_nonneg).2 fun x => ?_
    rw [‚Üê extend_apply f g‚ÇÅ h‚ÇÅ, ‚Üê extend_apply f g‚ÇÇ h‚ÇÇ]
    exact dist_coe_le_dist _
  ¬∑ refine (dist_le dist_nonneg).2 fun x => ?_
    calc
      dist (h‚ÇÅ x) (h‚ÇÇ x) = dist (extend f g‚ÇÅ h‚ÇÅ x) (extend f g‚ÇÇ h‚ÇÇ x) := by
        rw [extend_apply' x.coe_prop, extend_apply' x.coe_prop]
      _ ‚â§ _ := dist_coe_le_dist _
#align bounded_continuous_function.dist_extend_extend BoundedContinuousFunction.dist_extend_extend

theorem isometry_extend (f : Œ± ‚Ü™ Œ¥) (h : Œ¥ ‚Üí·µá Œ≤) : Isometry fun g : Œ± ‚Üí·µá Œ≤ => extend f g h :=
  Isometry.of_dist_eq fun g‚ÇÅ g‚ÇÇ => by simp [dist_nonneg]
#align bounded_continuous_function.isometry_extend BoundedContinuousFunction.isometry_extend

end Extend

end Basics

section ArzelaAscoli

variable [TopologicalSpace Œ±] [CompactSpace Œ±] [PseudoMetricSpace Œ≤]
variable {f g : Œ± ‚Üí·µá Œ≤} {x : Œ±} {C : ‚Ñù}

/- Arzela-Ascoli theorem asserts that, on a compact space, a set of functions sharing
a common modulus of continuity and taking values in a compact set forms a compact
subset for the topology of uniform convergence. In this section, we prove this theorem
and several useful variations around it. -/
/-- First version, with pointwise equicontinuity and range in a compact space. -/
theorem arzela_ascoli‚ÇÅ [CompactSpace Œ≤] (A : Set (Œ± ‚Üí·µá Œ≤)) (closed : IsClosed A)
    (H : Equicontinuous ((‚Üë) : A ‚Üí Œ± ‚Üí Œ≤)) : IsCompact A := by
  simp_rw [Equicontinuous, Metric.equicontinuousAt_iff_pair] at H
  refine isCompact_of_totallyBounded_isClosed ?_ closed
  refine totallyBounded_of_finite_discretization fun Œµ Œµ0 => ?_
  rcases exists_between Œµ0 with ‚ü®Œµ‚ÇÅ, Œµ‚ÇÅ0, ŒµŒµ‚ÇÅ‚ü©
  let Œµ‚ÇÇ := Œµ‚ÇÅ / 2 / 2
  /- We have to find a finite discretization of `u`, i.e., finite information
    that is sufficient to reconstruct `u` up to `Œµ`. This information will be
    provided by the values of `u` on a sufficiently dense set `tŒ±`,
    slightly translated to fit in a finite `Œµ‚ÇÇ`-dense set `tŒ≤` in the image. Such
    sets exist by compactness of the source and range. Then, to check that these
    data determine the function up to `Œµ`, one uses the control on the modulus of
    continuity to extend the closeness on tŒ± to closeness everywhere. -/
  have Œµ‚ÇÇ0 : Œµ‚ÇÇ > 0 := half_pos (half_pos Œµ‚ÇÅ0)
  have : ‚àÄ x : Œ±, ‚àÉ U, x ‚àà U ‚àß IsOpen U ‚àß
      ‚àÄ y ‚àà U, ‚àÄ z ‚àà U, ‚àÄ {f : Œ± ‚Üí·µá Œ≤}, f ‚àà A ‚Üí dist (f y) (f z) < Œµ‚ÇÇ := fun x =>
    let ‚ü®U, nhdsU, hU‚ü© := H x _ Œµ‚ÇÇ0
    let ‚ü®V, VU, openV, xV‚ü© := _root_.mem_nhds_iff.1 nhdsU
    ‚ü®V, xV, openV, fun y hy z hz f hf => hU y (VU hy) z (VU hz) ‚ü®f, hf‚ü©‚ü©
  choose U hU using this
  /- For all `x`, the set `hU x` is an open set containing `x` on which the elements of `A`
    fluctuate by at most `Œµ‚ÇÇ`.
    We extract finitely many of these sets that cover the whole space, by compactness. -/
  obtain ‚ü®tŒ± : Set Œ±, _, hfin, htŒ± : univ ‚äÜ ‚ãÉ x ‚àà tŒ±, U x‚ü© :=
    isCompact_univ.elim_finite_subcover_image (fun x _ => (hU x).2.1) fun x _ =>
      mem_biUnion (mem_univ _) (hU x).1
  rcases hfin.nonempty_fintype with ‚ü®_‚ü©
  obtain ‚ü®tŒ≤ : Set Œ≤, _, hfin, htŒ≤ : univ ‚äÜ ‚ãÉy ‚àà tŒ≤, ball y Œµ‚ÇÇ‚ü© :=
    @finite_cover_balls_of_compact Œ≤ _ _ isCompact_univ _ Œµ‚ÇÇ0
  rcases hfin.nonempty_fintype with ‚ü®_‚ü©
  -- Associate to every point `y` in the space a nearby point `F y` in `tŒ≤`
  choose F hF using fun y => show ‚àÉ z ‚àà tŒ≤, dist y z < Œµ‚ÇÇ by simpa using htŒ≤ (mem_univ y)
  -- `F : Œ≤ ‚Üí Œ≤`, `hF : ‚àÄ (y : Œ≤), F y ‚àà tŒ≤ ‚àß dist y (F y) < Œµ‚ÇÇ`
  /- Associate to every function a discrete approximation, mapping each point in `tŒ±`
    to a point in `tŒ≤` close to its true image by the function. -/
  refine ‚ü®tŒ± ‚Üí tŒ≤, by infer_instance, fun f a => ‚ü®F (f.1 a), (hF (f.1 a)).1‚ü©, ?_‚ü©
  rintro ‚ü®f, hf‚ü© ‚ü®g, hg‚ü© f_eq_g
  -- If two functions have the same approximation, then they are within distance `Œµ`
  refine lt_of_le_of_lt ((dist_le <| le_of_lt Œµ‚ÇÅ0).2 fun x => ?_) ŒµŒµ‚ÇÅ
  obtain ‚ü®x', x'tŒ±, hx'‚ü© := mem_iUnion‚ÇÇ.1 (htŒ± (mem_univ x))
  calc
    dist (f x) (g x) ‚â§ dist (f x) (f x') + dist (g x) (g x') + dist (f x') (g x') :=
      dist_triangle4_right _ _ _ _
    _ ‚â§ Œµ‚ÇÇ + Œµ‚ÇÇ + Œµ‚ÇÅ / 2 := by
      refine le_of_lt (add_lt_add (add_lt_add ?_ ?_) ?_)
      ¬∑ exact (hU x').2.2 _ hx' _ (hU x').1 hf
      ¬∑ exact (hU x').2.2 _ hx' _ (hU x').1 hg
      ¬∑ have F_f_g : F (f x') = F (g x') :=
          (congr_arg (fun f : tŒ± ‚Üí tŒ≤ => (f ‚ü®x', x'tŒ±‚ü© : Œ≤)) f_eq_g : _)
        calc
          dist (f x') (g x') ‚â§ dist (f x') (F (f x')) + dist (g x') (F (f x')) :=
            dist_triangle_right _ _ _
          _ = dist (f x') (F (f x')) + dist (g x') (F (g x')) := by rw [F_f_g]
          _ < Œµ‚ÇÇ + Œµ‚ÇÇ := (add_lt_add (hF (f x')).2 (hF (g x')).2)
          _ = Œµ‚ÇÅ / 2 := add_halves _
    _ = Œµ‚ÇÅ := by rw [add_halves, add_halves]
#align bounded_continuous_function.arzela_ascoli‚ÇÅ BoundedContinuousFunction.arzela_ascoli‚ÇÅ

/-- Second version, with pointwise equicontinuity and range in a compact subset. -/
theorem arzela_ascoli‚ÇÇ (s : Set Œ≤) (hs : IsCompact s) (A : Set (Œ± ‚Üí·µá Œ≤)) (closed : IsClosed A)
    (in_s : ‚àÄ (f : Œ± ‚Üí·µá Œ≤) (x : Œ±), f ‚àà A ‚Üí f x ‚àà s) (H : Equicontinuous ((‚Üë) : A ‚Üí Œ± ‚Üí Œ≤)) :
    IsCompact A := by
  /- This version is deduced from the previous one by restricting to the compact type in the target,
  using compactness there and then lifting everything to the original space. -/
  have M : LipschitzWith 1 Subtype.val := LipschitzWith.subtype_val s
  let F : (Œ± ‚Üí·µá s) ‚Üí Œ± ‚Üí·µá Œ≤ := comp (‚Üë) M
  refine IsCompact.of_isClosed_subset ((?_ : IsCompact (F ‚Åª¬π' A)).image (continuous_comp M)) closed
      fun f hf => ?_
  ¬∑ haveI : CompactSpace s := isCompact_iff_compactSpace.1 hs
    refine arzela_ascoli‚ÇÅ _ (continuous_iff_isClosed.1 (continuous_comp M) _ closed) ?_
    rw [uniformEmbedding_subtype_val.toUniformInducing.equicontinuous_iff]
    exact H.comp (A.restrictPreimage F)
  ¬∑ let g := codRestrict s f fun x => in_s f x hf
    rw [show f = F g by ext; rfl] at hf ‚ä¢
    exact ‚ü®g, hf, rfl‚ü©
#align bounded_continuous_function.arzela_ascoli‚ÇÇ BoundedContinuousFunction.arzela_ascoli‚ÇÇ

/-- Third (main) version, with pointwise equicontinuity and range in a compact subset, but
without closedness. The closure is then compact. -/
theorem arzela_ascoli [T2Space Œ≤] (s : Set Œ≤) (hs : IsCompact s) (A : Set (Œ± ‚Üí·µá Œ≤))
    (in_s : ‚àÄ (f : Œ± ‚Üí·µá Œ≤) (x : Œ±), f ‚àà A ‚Üí f x ‚àà s) (H : Equicontinuous ((‚Üë) : A ‚Üí Œ± ‚Üí Œ≤)) :
    IsCompact (closure A) :=
  /- This version is deduced from the previous one by checking that the closure of `A`, in
  addition to being closed, still satisfies the properties of compact range and equicontinuity. -/
  arzela_ascoli‚ÇÇ s hs (closure A) isClosed_closure
    (fun _ x hf =>
      (mem_of_closed' hs.isClosed).2 fun Œµ Œµ0 =>
        let ‚ü®g, gA, dist_fg‚ü© := Metric.mem_closure_iff.1 hf Œµ Œµ0
        ‚ü®g x, in_s g x gA, lt_of_le_of_lt (dist_coe_le_dist _) dist_fg‚ü©)
    (H.closure' continuous_coe)
#align bounded_continuous_function.arzela_ascoli BoundedContinuousFunction.arzela_ascoli

end ArzelaAscoli

section One

variable [TopologicalSpace Œ±] [PseudoMetricSpace Œ≤] [One Œ≤]

@[to_additive] instance instOne : One (Œ± ‚Üí·µá Œ≤) := ‚ü®const Œ± 1‚ü©

@[to_additive (attr := simp)]
theorem coe_one : ((1 : Œ± ‚Üí·µá Œ≤) : Œ± ‚Üí Œ≤) = 1 := rfl
#align bounded_continuous_function.coe_one BoundedContinuousFunction.coe_one
#align bounded_continuous_function.coe_zero BoundedContinuousFunction.coe_zero

@[to_additive (attr := simp)]
theorem mkOfCompact_one [CompactSpace Œ±] : mkOfCompact (1 : C(Œ±, Œ≤)) = 1 := rfl
#align bounded_continuous_function.mk_of_compact_one BoundedContinuousFunction.mkOfCompact_one
#align bounded_continuous_function.mk_of_compact_zero BoundedContinuousFunction.mkOfCompact_zero

@[to_additive]
theorem forall_coe_one_iff_one (f : Œ± ‚Üí·µá Œ≤) : (‚àÄ x, f x = 1) ‚Üî f = 1 :=
  (@DFunLike.ext_iff _ _ _ _ f 1).symm
#align bounded_continuous_function.forall_coe_one_iff_one BoundedContinuousFunction.forall_coe_one_iff_one
#align bounded_continuous_function.forall_coe_zero_iff_zero BoundedContinuousFunction.forall_coe_zero_iff_zero

@[to_additive (attr := simp)]
theorem one_compContinuous [TopologicalSpace Œ≥] (f : C(Œ≥, Œ±)) : (1 : Œ± ‚Üí·µá Œ≤).compContinuous f = 1 :=
  rfl
#align bounded_continuous_function.one_comp_continuous BoundedContinuousFunction.one_compContinuous
#align bounded_continuous_function.zero_comp_continuous BoundedContinuousFunction.zero_compContinuous

end One

section add

variable [TopologicalSpace Œ±] [PseudoMetricSpace Œ≤] [AddMonoid Œ≤] [BoundedAdd Œ≤] [ContinuousAdd Œ≤]
variable (f g : Œ± ‚Üí·µá Œ≤) {x : Œ±} {C : ‚Ñù}

/-- The pointwise sum of two bounded continuous functions is again bounded continuous. -/
instance instAdd : Add (Œ± ‚Üí·µá Œ≤) where
  add f g :=
    { toFun := fun x ‚Ü¶ f x + g x
      continuous_toFun := f.continuous.add g.continuous
      map_bounded' := add_bounded_of_bounded_of_bounded (map_bounded f) (map_bounded g) }

@[simp]
theorem coe_add : ‚áë(f + g) = f + g := rfl
#align bounded_continuous_function.coe_add BoundedContinuousFunction.coe_add

theorem add_apply : (f + g) x = f x + g x := rfl
#align bounded_continuous_function.add_apply BoundedContinuousFunction.add_apply

@[simp]
theorem mkOfCompact_add [CompactSpace Œ±] (f g : C(Œ±, Œ≤)) :
    mkOfCompact (f + g) = mkOfCompact f + mkOfCompact g := rfl
#align bounded_continuous_function.mk_of_compact_add BoundedContinuousFunction.mkOfCompact_add

theorem add_compContinuous [TopologicalSpace Œ≥] (h : C(Œ≥, Œ±)) :
    (g + f).compContinuous h = g.compContinuous h + f.compContinuous h := rfl
#align bounded_continuous_function.add_comp_continuous BoundedContinuousFunction.add_compContinuous

@[simp]
theorem coe_nsmulRec : ‚àÄ n, ‚áë(nsmulRec n f) = n ‚Ä¢ ‚áëf
  | 0 => by rw [nsmulRec, zero_smul, coe_zero]
  | n + 1 => by rw [nsmulRec, succ_nsmul, coe_add, coe_nsmulRec n]
#align bounded_continuous_function.coe_nsmul_rec BoundedContinuousFunction.coe_nsmulRec

instance instSMulNat : SMul ‚Ñï (Œ± ‚Üí·µá Œ≤) where
  smul n f :=
    { toContinuousMap := n ‚Ä¢ f.toContinuousMap
      map_bounded' := by simpa [coe_nsmulRec] using (nsmulRec n f).map_bounded' }
#align bounded_continuous_function.has_nat_scalar BoundedContinuousFunction.instSMulNat

@[simp]
theorem coe_nsmul (r : ‚Ñï) (f : Œ± ‚Üí·µá Œ≤) : ‚áë(r ‚Ä¢ f) = r ‚Ä¢ ‚áëf := rfl
#align bounded_continuous_function.coe_nsmul BoundedContinuousFunction.coe_nsmul

@[simp]
theorem nsmul_apply (r : ‚Ñï) (f : Œ± ‚Üí·µá Œ≤) (v : Œ±) : (r ‚Ä¢ f) v = r ‚Ä¢ f v := rfl
#align bounded_continuous_function.nsmul_apply BoundedContinuousFunction.nsmul_apply

instance instAddMonoid : AddMonoid (Œ± ‚Üí·µá Œ≤) :=
  DFunLike.coe_injective.addMonoid _ coe_zero coe_add fun _ _ => coe_nsmul _ _

/-- Coercion of a `NormedAddGroupHom` is an `AddMonoidHom`. Similar to `AddMonoidHom.coeFn`. -/
@[simps]
def coeFnAddHom : (Œ± ‚Üí·µá Œ≤) ‚Üí+ Œ± ‚Üí Œ≤ where
  toFun := (‚áë)
  map_zero' := coe_zero
  map_add' := coe_add
#align bounded_continuous_function.coe_fn_add_hom BoundedContinuousFunction.coeFnAddHom

variable (Œ± Œ≤)

/-- The additive map forgetting that a bounded continuous function is bounded. -/
@[simps]
def toContinuousMapAddHom : (Œ± ‚Üí·µá Œ≤) ‚Üí+ C(Œ±, Œ≤) where
  toFun := toContinuousMap
  map_zero' := rfl
  map_add' := by
    intros
    ext
    simp
#align bounded_continuous_function.to_continuous_map_add_hom BoundedContinuousFunction.toContinuousMapAddHom

end add

section comm_add

variable [TopologicalSpace Œ±]
variable [PseudoMetricSpace Œ≤] [AddCommMonoid Œ≤] [BoundedAdd Œ≤] [ContinuousAdd Œ≤]

@[to_additive]
instance instAddCommMonoid : AddCommMonoid (Œ± ‚Üí·µá Œ≤) where
  add_comm f g := by ext; simp [add_comm]

@[simp]
theorem coe_sum {Œπ : Type*} (s : Finset Œπ) (f : Œπ ‚Üí Œ± ‚Üí·µá Œ≤) :
    ‚áë(‚àë i ‚àà s, f i) = ‚àë i ‚àà s, (f i : Œ± ‚Üí Œ≤) :=
  map_sum coeFnAddHom f s
#align bounded_continuous_function.coe_sum BoundedContinuousFunction.coe_sum

theorem sum_apply {Œπ : Type*} (s : Finset Œπ) (f : Œπ ‚Üí Œ± ‚Üí·µá Œ≤) (a : Œ±) :
    (‚àë i ‚àà s, f i) a = ‚àë i ‚àà s, f i a := by simp
#align bounded_continuous_function.sum_apply BoundedContinuousFunction.sum_apply

end comm_add

section LipschitzAdd

/- In this section, if `Œ≤` is an `AddMonoid` whose addition operation is Lipschitz, then we show
that the space of bounded continuous functions from `Œ±` to `Œ≤` inherits a topological `AddMonoid`
structure, by using pointwise operations and checking that they are compatible with the uniform
distance.

Implementation note: The material in this section could have been written for `LipschitzMul`
and transported by `@[to_additive]`. We choose not to do this because this causes a few lemma
names (for example, `coe_mul`) to conflict with later lemma names for normed rings; this is only a
trivial inconvenience, but in any case there are no obvious applications of the multiplicative
version. -/

variable [TopologicalSpace Œ±] [PseudoMetricSpace Œ≤] [AddMonoid Œ≤] [LipschitzAdd Œ≤]
variable (f g : Œ± ‚Üí·µá Œ≤) {x : Œ±} {C : ‚Ñù}

instance instLipschitzAdd : LipschitzAdd (Œ± ‚Üí·µá Œ≤) where
  lipschitz_add :=
    ‚ü®LipschitzAdd.C Œ≤, by
      have C_nonneg := (LipschitzAdd.C Œ≤).coe_nonneg
      rw [lipschitzWith_iff_dist_le_mul]
      rintro ‚ü®f‚ÇÅ, g‚ÇÅ‚ü© ‚ü®f‚ÇÇ, g‚ÇÇ‚ü©
      rw [dist_le (mul_nonneg C_nonneg dist_nonneg)]
      intro x
      refine le_trans (lipschitz_with_lipschitz_const_add ‚ü®f‚ÇÅ x, g‚ÇÅ x‚ü© ‚ü®f‚ÇÇ x, g‚ÇÇ x‚ü©) ?_
      refine mul_le_mul_of_nonneg_left ?_ C_nonneg
      apply max_le_max <;> exact dist_coe_le_dist x‚ü©

end LipschitzAdd

section sub

variable [TopologicalSpace Œ±]
variable {R : Type*} [PseudoMetricSpace R] [Sub R] [BoundedSub R] [ContinuousSub R]
variable (f g : Œ± ‚Üí·µá R)

/-- The pointwise difference of two bounded continuous functions is again bounded continuous. -/
instance instSub : Sub (Œ± ‚Üí·µá R) where
  sub f g :=
    { toFun := fun x ‚Ü¶ (f x - g x),
      map_bounded' := sub_bounded_of_bounded_of_bounded f.map_bounded' g.map_bounded' }

theorem sub_apply {x : Œ±} : (f - g) x = f x - g x := rfl
#align bounded_continuous_function.sub_apply BoundedContinuousFunction.sub_apply

@[simp]
theorem coe_sub : ‚áë(f - g) = f - g := rfl
#align bounded_continuous_function.coe_sub BoundedContinuousFunction.coe_sub

end sub

section casts

variable [TopologicalSpace Œ±] {Œ≤ : Type*} [PseudoMetricSpace Œ≤]

instance [NatCast Œ≤] : NatCast (Œ± ‚Üí·µá Œ≤) := ‚ü®fun n ‚Ü¶ BoundedContinuousFunction.const _ n‚ü©

@[simp]
theorem natCast_apply [NatCast Œ≤] (n : ‚Ñï) (x : Œ±) : (n : Œ± ‚Üí·µá Œ≤) x = n := rfl

instance [IntCast Œ≤] : IntCast (Œ± ‚Üí·µá Œ≤) := ‚ü®fun m ‚Ü¶ BoundedContinuousFunction.const _ m‚ü©

@[simp]
theorem intCast_apply [IntCast Œ≤] (m : ‚Ñ§) (x : Œ±) : (m : Œ± ‚Üí·µá Œ≤) x = m := rfl

end casts

section mul

variable [TopologicalSpace Œ±] {R : Type*} [PseudoMetricSpace R]

instance instMul [Mul R] [BoundedMul R] [ContinuousMul R] :
    Mul (Œ± ‚Üí·µá R) where
  mul f g :=
    { toFun := fun x ‚Ü¶ f x * g x
      continuous_toFun := f.continuous.mul g.continuous
      map_bounded' := mul_bounded_of_bounded_of_bounded (map_bounded f) (map_bounded g) }

@[simp]
theorem coe_mul [Mul R] [BoundedMul R] [ContinuousMul R] (f g : Œ± ‚Üí·µá R) : ‚áë(f * g) = f * g := rfl
#align bounded_continuous_function.coe_mul BoundedContinuousFunction.coe_mul

theorem mul_apply [Mul R] [BoundedMul R] [ContinuousMul R] (f g : Œ± ‚Üí·µá R) (x : Œ±) :
    (f * g) x = f x * g x := rfl
#align bounded_continuous_function.mul_apply BoundedContinuousFunction.mul_apply

instance instPow [Monoid R] [BoundedMul R] [ContinuousMul R] : Pow (Œ± ‚Üí·µá R) ‚Ñï where
  pow f n :=
    { toFun := fun x ‚Ü¶ (f x) ^ n
      continuous_toFun := f.continuous.pow n
      map_bounded' := by
        obtain ‚ü®C, hC‚ü© := Metric.isBounded_iff.mp <| isBounded_pow (isBounded_range f) n
        exact ‚ü®C, fun x y ‚Ü¶ hC (by simp) (by simp)‚ü© }

theorem coe_pow [Monoid R] [BoundedMul R] [ContinuousMul R] (n : ‚Ñï) (f : Œ± ‚Üí·µá R) :
    ‚áë(f ^ n) = (‚áëf) ^ n := rfl
#align bounded_continuous_function.coe_pow BoundedContinuousFunction.coe_pow

@[simp]
theorem pow_apply [Monoid R] [BoundedMul R] [ContinuousMul R] (n : ‚Ñï) (f : Œ± ‚Üí·µá R) (x : Œ±) :
    (f ^ n) x = f x ^ n := rfl
#align bounded_continuous_function.pow_apply BoundedContinuousFunction.pow_apply

instance instMonoid [Monoid R] [BoundedMul R] [ContinuousMul R] :
    Monoid (Œ± ‚Üí·µá R) :=
  Injective.monoid (‚Üë) DFunLike.coe_injective' rfl (fun _ _ ‚Ü¶ rfl) (fun _ _ ‚Ü¶ rfl)

instance instCommMonoid [CommMonoid R] [BoundedMul R] [ContinuousMul R] :
    CommMonoid (Œ± ‚Üí·µá R) where
  __ := instMonoid
  mul_comm f g := by ext x; simp [mul_apply, mul_comm]

instance instSemiring [Semiring R] [BoundedMul R] [ContinuousMul R]
    [BoundedAdd R] [ContinuousAdd R] :
    Semiring (Œ± ‚Üí·µá R) :=
  Injective.semiring (‚Üë) DFunLike.coe_injective'
    rfl rfl (fun _ _ ‚Ü¶ rfl) (fun _ _ ‚Ü¶ rfl) (fun _ _ ‚Ü¶ rfl) (fun _ _ ‚Ü¶ rfl) (fun _ ‚Ü¶ rfl)

end mul

section NormedAddCommGroup

/- In this section, if `Œ≤` is a normed group, then we show that the space of bounded
continuous functions from `Œ±` to `Œ≤` inherits a normed group structure, by using
pointwise operations and checking that they are compatible with the uniform distance. -/
variable [TopologicalSpace Œ±] [SeminormedAddCommGroup Œ≤]
variable (f g : Œ± ‚Üí·µá Œ≤) {x : Œ±} {C : ‚Ñù}

instance instNorm : Norm (Œ± ‚Üí·µá Œ≤) := ‚ü®(dist ¬∑ 0)‚ü©

theorem norm_def : ‚Äñf‚Äñ = dist f 0 := rfl
#align bounded_continuous_function.norm_def BoundedContinuousFunction.norm_def

/-- The norm of a bounded continuous function is the supremum of `‚Äñf x‚Äñ`.
We use `sInf` to ensure that the definition works if `Œ±` has no elements. -/
theorem norm_eq (f : Œ± ‚Üí·µá Œ≤) : ‚Äñf‚Äñ = sInf { C : ‚Ñù | 0 ‚â§ C ‚àß ‚àÄ x : Œ±, ‚Äñf x‚Äñ ‚â§ C } := by
  simp [norm_def, BoundedContinuousFunction.dist_eq]
#align bounded_continuous_function.norm_eq BoundedContinuousFunction.norm_eq

/-- When the domain is non-empty, we do not need the `0 ‚â§ C` condition in the formula for `‚Äñf‚Äñ` as a
`sInf`. -/
theorem norm_eq_of_nonempty [h : Nonempty Œ±] : ‚Äñf‚Äñ = sInf { C : ‚Ñù | ‚àÄ x : Œ±, ‚Äñf x‚Äñ ‚â§ C } := by
  obtain ‚ü®a‚ü© := h
  rw [norm_eq]
  congr
  ext
  simp only [mem_setOf_eq, and_iff_right_iff_imp]
  exact fun h' => le_trans (norm_nonneg (f a)) (h' a)
#align bounded_continuous_function.norm_eq_of_nonempty BoundedContinuousFunction.norm_eq_of_nonempty

@[simp]
theorem norm_eq_zero_of_empty [IsEmpty Œ±] : ‚Äñf‚Äñ = 0 :=
  dist_zero_of_empty
#align bounded_continuous_function.norm_eq_zero_of_empty BoundedContinuousFunction.norm_eq_zero_of_empty

theorem norm_coe_le_norm (x : Œ±) : ‚Äñf x‚Äñ ‚â§ ‚Äñf‚Äñ :=
  calc
    ‚Äñf x‚Äñ = dist (f x) ((0 : Œ± ‚Üí·µá Œ≤) x) := by simp [dist_zero_right]
    _ ‚â§ ‚Äñf‚Äñ := dist_coe_le_dist _
#align bounded_continuous_function.norm_coe_le_norm BoundedContinuousFunction.norm_coe_le_norm

lemma neg_norm_le_apply (f : Œ± ‚Üí·µá ‚Ñù) (x : Œ±) :
    -‚Äñf‚Äñ ‚â§ f x := (abs_le.mp (norm_coe_le_norm f x)).1

lemma apply_le_norm (f : Œ± ‚Üí·µá ‚Ñù) (x : Œ±) :
    f x ‚â§ ‚Äñf‚Äñ := (abs_le.mp (norm_coe_le_norm f x)).2

theorem dist_le_two_norm' {f : Œ≥ ‚Üí Œ≤} {C : ‚Ñù} (hC : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C) (x y : Œ≥) :
    dist (f x) (f y) ‚â§ 2 * C :=
  calc
    dist (f x) (f y) ‚â§ ‚Äñf x‚Äñ + ‚Äñf y‚Äñ := dist_le_norm_add_norm _ _
    _ ‚â§ C + C := add_le_add (hC x) (hC y)
    _ = 2 * C := (two_mul _).symm
#align bounded_continuous_function.dist_le_two_norm' BoundedContinuousFunction.dist_le_two_norm'

/-- Distance between the images of any two points is at most twice the norm of the function. -/
theorem dist_le_two_norm (x y : Œ±) : dist (f x) (f y) ‚â§ 2 * ‚Äñf‚Äñ :=
  dist_le_two_norm' f.norm_coe_le_norm x y
#align bounded_continuous_function.dist_le_two_norm BoundedContinuousFunction.dist_le_two_norm

variable {f}

/-- The norm of a function is controlled by the supremum of the pointwise norms. -/
theorem norm_le (C0 : (0 : ‚Ñù) ‚â§ C) : ‚Äñf‚Äñ ‚â§ C ‚Üî ‚àÄ x : Œ±, ‚Äñf x‚Äñ ‚â§ C := by
  simpa using @dist_le _ _ _ _ f 0 _ C0
#align bounded_continuous_function.norm_le BoundedContinuousFunction.norm_le

theorem norm_le_of_nonempty [Nonempty Œ±] {f : Œ± ‚Üí·µá Œ≤} {M : ‚Ñù} : ‚Äñf‚Äñ ‚â§ M ‚Üî ‚àÄ x, ‚Äñf x‚Äñ ‚â§ M := by
  simp_rw [norm_def, ‚Üê dist_zero_right]
  exact dist_le_iff_of_nonempty
#align bounded_continuous_function.norm_le_of_nonempty BoundedContinuousFunction.norm_le_of_nonempty

theorem norm_lt_iff_of_compact [CompactSpace Œ±] {f : Œ± ‚Üí·µá Œ≤} {M : ‚Ñù} (M0 : 0 < M) :
    ‚Äñf‚Äñ < M ‚Üî ‚àÄ x, ‚Äñf x‚Äñ < M := by
  simp_rw [norm_def, ‚Üê dist_zero_right]
  exact dist_lt_iff_of_compact M0
#align bounded_continuous_function.norm_lt_iff_of_compact BoundedContinuousFunction.norm_lt_iff_of_compact

theorem norm_lt_iff_of_nonempty_compact [Nonempty Œ±] [CompactSpace Œ±] {f : Œ± ‚Üí·µá Œ≤} {M : ‚Ñù} :
    ‚Äñf‚Äñ < M ‚Üî ‚àÄ x, ‚Äñf x‚Äñ < M := by
  simp_rw [norm_def, ‚Üê dist_zero_right]
  exact dist_lt_iff_of_nonempty_compact
#align bounded_continuous_function.norm_lt_iff_of_nonempty_compact BoundedContinuousFunction.norm_lt_iff_of_nonempty_compact

variable (f)

/-- Norm of `const Œ± b` is less than or equal to `‚Äñb‚Äñ`. If `Œ±` is nonempty,
then it is equal to `‚Äñb‚Äñ`. -/
theorem norm_const_le (b : Œ≤) : ‚Äñconst Œ± b‚Äñ ‚â§ ‚Äñb‚Äñ :=
  (norm_le (norm_nonneg b)).2 fun _ => le_rfl
#align bounded_continuous_function.norm_const_le BoundedContinuousFunction.norm_const_le

@[simp]
theorem norm_const_eq [h : Nonempty Œ±] (b : Œ≤) : ‚Äñconst Œ± b‚Äñ = ‚Äñb‚Äñ :=
  le_antisymm (norm_const_le b) <| h.elim fun x => (const Œ± b).norm_coe_le_norm x
#align bounded_continuous_function.norm_const_eq BoundedContinuousFunction.norm_const_eq

/-- Constructing a bounded continuous function from a uniformly bounded continuous
function taking values in a normed group. -/
def ofNormedAddCommGroup {Œ± : Type u} {Œ≤ : Type v} [TopologicalSpace Œ±] [SeminormedAddCommGroup Œ≤]
    (f : Œ± ‚Üí Œ≤) (Hf : Continuous f) (C : ‚Ñù) (H : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C) : Œ± ‚Üí·µá Œ≤ :=
  ‚ü®‚ü®fun n => f n, Hf‚ü©, ‚ü®_, dist_le_two_norm' H‚ü©‚ü©
#align bounded_continuous_function.of_normed_add_comm_group BoundedContinuousFunction.ofNormedAddCommGroup

@[simp]
theorem coe_ofNormedAddCommGroup {Œ± : Type u} {Œ≤ : Type v} [TopologicalSpace Œ±]
    [SeminormedAddCommGroup Œ≤] (f : Œ± ‚Üí Œ≤) (Hf : Continuous f) (C : ‚Ñù) (H : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C) :
    (ofNormedAddCommGroup f Hf C H : Œ± ‚Üí Œ≤) = f := rfl
#align bounded_continuous_function.coe_of_normed_add_comm_group BoundedContinuousFunction.coe_ofNormedAddCommGroup

theorem norm_ofNormedAddCommGroup_le {f : Œ± ‚Üí Œ≤} (hfc : Continuous f) {C : ‚Ñù} (hC : 0 ‚â§ C)
    (hfC : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C) : ‚ÄñofNormedAddCommGroup f hfc C hfC‚Äñ ‚â§ C :=
  (norm_le hC).2 hfC
#align bounded_continuous_function.norm_of_normed_add_comm_group_le BoundedContinuousFunction.norm_ofNormedAddCommGroup_le

/-- Constructing a bounded continuous function from a uniformly bounded
function on a discrete space, taking values in a normed group. -/
def ofNormedAddCommGroupDiscrete {Œ± : Type u} {Œ≤ : Type v} [TopologicalSpace Œ±] [DiscreteTopology Œ±]
    [SeminormedAddCommGroup Œ≤] (f : Œ± ‚Üí Œ≤) (C : ‚Ñù) (H : ‚àÄ x, norm (f x) ‚â§ C) : Œ± ‚Üí·µá Œ≤ :=
  ofNormedAddCommGroup f continuous_of_discreteTopology C H
#align bounded_continuous_function.of_normed_add_comm_group_discrete BoundedContinuousFunction.ofNormedAddCommGroupDiscrete

@[simp]
theorem coe_ofNormedAddCommGroupDiscrete {Œ± : Type u} {Œ≤ : Type v} [TopologicalSpace Œ±]
    [DiscreteTopology Œ±] [SeminormedAddCommGroup Œ≤] (f : Œ± ‚Üí Œ≤) (C : ‚Ñù) (H : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C) :
    (ofNormedAddCommGroupDiscrete f C H : Œ± ‚Üí Œ≤) = f := rfl
#align bounded_continuous_function.coe_of_normed_add_comm_group_discrete BoundedContinuousFunction.coe_ofNormedAddCommGroupDiscrete

/-- Taking the pointwise norm of a bounded continuous function with values in a
`SeminormedAddCommGroup` yields a bounded continuous function with values in ‚Ñù. -/
def normComp : Œ± ‚Üí·µá ‚Ñù :=
  f.comp norm lipschitzWith_one_norm
#align bounded_continuous_function.norm_comp BoundedContinuousFunction.normComp

@[simp]
theorem coe_normComp : (f.normComp : Œ± ‚Üí ‚Ñù) = norm ‚àò f := rfl
#align bounded_continuous_function.coe_norm_comp BoundedContinuousFunction.coe_normComp

@[simp]
theorem norm_normComp : ‚Äñf.normComp‚Äñ = ‚Äñf‚Äñ := by
  simp only [norm_eq, coe_normComp, norm_norm, Function.comp]
#align bounded_continuous_function.norm_norm_comp BoundedContinuousFunction.norm_normComp

theorem bddAbove_range_norm_comp : BddAbove <| Set.range <| norm ‚àò f :=
  (@isBounded_range _ _ _ _ f.normComp).bddAbove
#align bounded_continuous_function.bdd_above_range_norm_comp BoundedContinuousFunction.bddAbove_range_norm_comp

theorem norm_eq_iSup_norm : ‚Äñf‚Äñ = ‚®Ü x : Œ±, ‚Äñf x‚Äñ := by
  simp_rw [norm_def, dist_eq_iSup, coe_zero, Pi.zero_apply, dist_zero_right]
#align bounded_continuous_function.norm_eq_supr_norm BoundedContinuousFunction.norm_eq_iSup_norm

/-- If `‚Äñ(1 : Œ≤)‚Äñ = 1`, then `‚Äñ(1 : Œ± ‚Üí·µá Œ≤)‚Äñ = 1` if `Œ±` is nonempty. -/
instance instNormOneClass [Nonempty Œ±] [One Œ≤] [NormOneClass Œ≤] : NormOneClass (Œ± ‚Üí·µá Œ≤) where
  norm_one := by simp only [norm_eq_iSup_norm, coe_one, Pi.one_apply, norm_one, ciSup_const]

/-- The pointwise opposite of a bounded continuous function is again bounded continuous. -/
instance : Neg (Œ± ‚Üí·µá Œ≤) :=
  ‚ü®fun f =>
    ofNormedAddCommGroup (-f) f.continuous.neg ‚Äñf‚Äñ fun x =>
      norm_neg ((‚áëf) x) ‚ñ∏ f.norm_coe_le_norm x‚ü©

@[simp]
theorem coe_neg : ‚áë(-f) = -f := rfl
#align bounded_continuous_function.coe_neg BoundedContinuousFunction.coe_neg

theorem neg_apply : (-f) x = -f x := rfl
#align bounded_continuous_function.neg_apply BoundedContinuousFunction.neg_apply

@[simp]
theorem mkOfCompact_neg [CompactSpace Œ±] (f : C(Œ±, Œ≤)) : mkOfCompact (-f) = -mkOfCompact f := rfl
#align bounded_continuous_function.mk_of_compact_neg BoundedContinuousFunction.mkOfCompact_neg

@[simp]
theorem mkOfCompact_sub [CompactSpace Œ±] (f g : C(Œ±, Œ≤)) :
    mkOfCompact (f - g) = mkOfCompact f - mkOfCompact g := rfl
#align bounded_continuous_function.mk_of_compact_sub BoundedContinuousFunction.mkOfCompact_sub

@[simp]
theorem coe_zsmulRec : ‚àÄ z, ‚áë(zsmulRec (¬∑ ‚Ä¢ ¬∑) z f) = z ‚Ä¢ ‚áëf
  | Int.ofNat n => by rw [zsmulRec, Int.ofNat_eq_coe, coe_nsmul, natCast_zsmul]
  | Int.negSucc n => by rw [zsmulRec, negSucc_zsmul, coe_neg, coe_nsmul]
#align bounded_continuous_function.coe_zsmul_rec BoundedContinuousFunction.coe_zsmulRec

instance instSMulInt : SMul ‚Ñ§ (Œ± ‚Üí·µá Œ≤) where
  smul n f :=
    { toContinuousMap := n ‚Ä¢ f.toContinuousMap
      map_bounded' := by simpa using (zsmulRec (¬∑ ‚Ä¢ ¬∑) n f).map_bounded' }
#align bounded_continuous_function.has_int_scalar BoundedContinuousFunction.instSMulInt

@[simp]
theorem coe_zsmul (r : ‚Ñ§) (f : Œ± ‚Üí·µá Œ≤) : ‚áë(r ‚Ä¢ f) = r ‚Ä¢ ‚áëf := rfl
#align bounded_continuous_function.coe_zsmul BoundedContinuousFunction.coe_zsmul

@[simp]
theorem zsmul_apply (r : ‚Ñ§) (f : Œ± ‚Üí·µá Œ≤) (v : Œ±) : (r ‚Ä¢ f) v = r ‚Ä¢ f v := rfl
#align bounded_continuous_function.zsmul_apply BoundedContinuousFunction.zsmul_apply

instance instAddCommGroup : AddCommGroup (Œ± ‚Üí·µá Œ≤) :=
  DFunLike.coe_injective.addCommGroup _ coe_zero coe_add coe_neg coe_sub (fun _ _ => coe_nsmul _ _)
    fun _ _ => coe_zsmul _ _

instance instSeminormedAddCommGroup : SeminormedAddCommGroup (Œ± ‚Üí·µá Œ≤) where
  dist_eq f g := by simp only [norm_eq, dist_eq, dist_eq_norm, sub_apply]

instance instNormedAddCommGroup {Œ± Œ≤} [TopologicalSpace Œ±] [NormedAddCommGroup Œ≤] :
    NormedAddCommGroup (Œ± ‚Üí·µá Œ≤) :=
  { instSeminormedAddCommGroup with
    -- Porting note (#10888): Added a proof for `eq_of_dist_eq_zero`
    eq_of_dist_eq_zero }

theorem nnnorm_def : ‚Äñf‚Äñ‚Çä = nndist f 0 := rfl
#align bounded_continuous_function.nnnorm_def BoundedContinuousFunction.nnnorm_def

theorem nnnorm_coe_le_nnnorm (x : Œ±) : ‚Äñf x‚Äñ‚Çä ‚â§ ‚Äñf‚Äñ‚Çä :=
  norm_coe_le_norm _ _
#align bounded_continuous_function.nnnorm_coe_le_nnnorm BoundedContinuousFunction.nnnorm_coe_le_nnnorm

theorem nndist_le_two_nnnorm (x y : Œ±) : nndist (f x) (f y) ‚â§ 2 * ‚Äñf‚Äñ‚Çä :=
  dist_le_two_norm _ _ _
#align bounded_continuous_function.nndist_le_two_nnnorm BoundedContinuousFunction.nndist_le_two_nnnorm

/-- The `nnnorm` of a function is controlled by the supremum of the pointwise `nnnorm`s. -/
theorem nnnorm_le (C : ‚Ñù‚â•0) : ‚Äñf‚Äñ‚Çä ‚â§ C ‚Üî ‚àÄ x : Œ±, ‚Äñf x‚Äñ‚Çä ‚â§ C :=
  norm_le C.prop
#align bounded_continuous_function.nnnorm_le BoundedContinuousFunction.nnnorm_le

theorem nnnorm_const_le (b : Œ≤) : ‚Äñconst Œ± b‚Äñ‚Çä ‚â§ ‚Äñb‚Äñ‚Çä :=
  norm_const_le _
#align bounded_continuous_function.nnnorm_const_le BoundedContinuousFunction.nnnorm_const_le

@[simp]
theorem nnnorm_const_eq [Nonempty Œ±] (b : Œ≤) : ‚Äñconst Œ± b‚Äñ‚Çä = ‚Äñb‚Äñ‚Çä :=
  Subtype.ext <| norm_const_eq _
#align bounded_continuous_function.nnnorm_const_eq BoundedContinuousFunction.nnnorm_const_eq

theorem nnnorm_eq_iSup_nnnorm : ‚Äñf‚Äñ‚Çä = ‚®Ü x : Œ±, ‚Äñf x‚Äñ‚Çä :=
  Subtype.ext <| (norm_eq_iSup_norm f).trans <| by simp_rw [val_eq_coe, NNReal.coe_iSup, coe_nnnorm]
#align bounded_continuous_function.nnnorm_eq_supr_nnnorm BoundedContinuousFunction.nnnorm_eq_iSup_nnnorm

theorem abs_diff_coe_le_dist : ‚Äñf x - g x‚Äñ ‚â§ dist f g := by
  rw [dist_eq_norm]
  exact (f - g).norm_coe_le_norm x
#align bounded_continuous_function.abs_diff_coe_le_dist BoundedContinuousFunction.abs_diff_coe_le_dist

theorem coe_le_coe_add_dist {f g : Œ± ‚Üí·µá ‚Ñù} : f x ‚â§ g x + dist f g :=
  sub_le_iff_le_add'.1 <| (abs_le.1 <| @dist_coe_le_dist _ _ _ _ f g x).2
#align bounded_continuous_function.coe_le_coe_add_dist BoundedContinuousFunction.coe_le_coe_add_dist

theorem norm_compContinuous_le [TopologicalSpace Œ≥] (f : Œ± ‚Üí·µá Œ≤) (g : C(Œ≥, Œ±)) :
    ‚Äñf.compContinuous g‚Äñ ‚â§ ‚Äñf‚Äñ :=
  ((lipschitz_compContinuous g).dist_le_mul f 0).trans <| by
    rw [NNReal.coe_one, one_mul, dist_zero_right]
#align bounded_continuous_function.norm_comp_continuous_le BoundedContinuousFunction.norm_compContinuous_le

end NormedAddCommGroup

section BoundedSMul

/-!
### `BoundedSMul` (in particular, topological module) structure

In this section, if `Œ≤` is a metric space and a `ùïú`-module whose addition and scalar multiplication
are compatible with the metric structure, then we show that the space of bounded continuous
functions from `Œ±` to `Œ≤` inherits a so-called `BoundedSMul` structure (in particular, a
`ContinuousMul` structure, which is the mathlib formulation of being a topological module), by
using pointwise operations and checking that they are compatible with the uniform distance. -/


variable {ùïú : Type*} [PseudoMetricSpace ùïú] [TopologicalSpace Œ±] [PseudoMetricSpace Œ≤]

section SMul

variable [Zero ùïú] [Zero Œ≤] [SMul ùïú Œ≤] [BoundedSMul ùïú Œ≤]

instance instSMul : SMul ùïú (Œ± ‚Üí·µá Œ≤) where
  smul c f :=
    { toContinuousMap := c ‚Ä¢ f.toContinuousMap
      map_bounded' :=
        let ‚ü®b, hb‚ü© := f.bounded
        ‚ü®dist c 0 * b, fun x y => by
          refine (dist_smul_pair c (f x) (f y)).trans ?_
          refine mul_le_mul_of_nonneg_left ?_ dist_nonneg
          exact hb x y‚ü© }

@[simp]
theorem coe_smul (c : ùïú) (f : Œ± ‚Üí·µá Œ≤) : ‚áë(c ‚Ä¢ f) = fun x => c ‚Ä¢ f x := rfl
#align bounded_continuous_function.coe_smul BoundedContinuousFunction.coe_smul

theorem smul_apply (c : ùïú) (f : Œ± ‚Üí·µá Œ≤) (x : Œ±) : (c ‚Ä¢ f) x = c ‚Ä¢ f x := rfl
#align bounded_continuous_function.smul_apply BoundedContinuousFunction.smul_apply

instance instIsCentralScalar [SMul ùïú·µê·µí·µñ Œ≤] [IsCentralScalar ùïú Œ≤] : IsCentralScalar ùïú (Œ± ‚Üí·µá Œ≤) where
  op_smul_eq_smul _ _ := ext fun _ => op_smul_eq_smul _ _

instance instBoundedSMul : BoundedSMul ùïú (Œ± ‚Üí·µá Œ≤) where
  dist_smul_pair' c f‚ÇÅ f‚ÇÇ := by
    rw [dist_le (mul_nonneg dist_nonneg dist_nonneg)]
    intro x
    refine (dist_smul_pair c (f‚ÇÅ x) (f‚ÇÇ x)).trans ?_
    exact mul_le_mul_of_nonneg_left (dist_coe_le_dist x) dist_nonneg
  dist_pair_smul' c‚ÇÅ c‚ÇÇ f := by
    rw [dist_le (mul_nonneg dist_nonneg dist_nonneg)]
    intro x
    refine (dist_pair_smul c‚ÇÅ c‚ÇÇ (f x)).trans ?_
    refine mul_le_mul_of_nonneg_left ?_ dist_nonneg
    convert dist_coe_le_dist (Œ≤ := Œ≤) x
    simp

end SMul

section MulAction

variable [MonoidWithZero ùïú] [Zero Œ≤] [MulAction ùïú Œ≤] [BoundedSMul ùïú Œ≤]

instance instMulAction : MulAction ùïú (Œ± ‚Üí·µá Œ≤) :=
  DFunLike.coe_injective.mulAction _ coe_smul

end MulAction

section DistribMulAction

variable [MonoidWithZero ùïú] [AddMonoid Œ≤] [DistribMulAction ùïú Œ≤] [BoundedSMul ùïú Œ≤]
variable [BoundedAdd Œ≤] [ContinuousAdd Œ≤]

instance instDistribMulAction : DistribMulAction ùïú (Œ± ‚Üí·µá Œ≤) :=
  DFunLike.coe_injective.distribMulAction ‚ü®‚ü®_, coe_zero‚ü©, coe_add‚ü© coe_smul

end DistribMulAction

section Module

variable [Semiring ùïú] [AddCommMonoid Œ≤] [Module ùïú Œ≤] [BoundedSMul ùïú Œ≤]
variable {f g : Œ± ‚Üí·µá Œ≤} {x : Œ±} {C : ‚Ñù}
variable [BoundedAdd Œ≤] [ContinuousAdd Œ≤]

instance instModule : Module ùïú (Œ± ‚Üí·µá Œ≤) :=
  DFunLike.coe_injective.module _ ‚ü®‚ü®_, coe_zero‚ü©, coe_add‚ü©  coe_smul

variable (ùïú)

/-- The evaluation at a point, as a continuous linear map from `Œ± ‚Üí·µá Œ≤` to `Œ≤`. -/
def evalCLM (x : Œ±) : (Œ± ‚Üí·µá Œ≤) ‚ÜíL[ùïú] Œ≤ where
  toFun f := f x
  map_add' f g := add_apply _ _
  map_smul' c f := smul_apply _ _ _
#align bounded_continuous_function.eval_clm BoundedContinuousFunction.evalCLM

@[simp]
theorem evalCLM_apply (x : Œ±) (f : Œ± ‚Üí·µá Œ≤) : evalCLM ùïú x f = f x := rfl
#align bounded_continuous_function.eval_clm_apply BoundedContinuousFunction.evalCLM_apply

variable (Œ± Œ≤)

/-- The linear map forgetting that a bounded continuous function is bounded. -/
@[simps]
def toContinuousMapLinearMap : (Œ± ‚Üí·µá Œ≤) ‚Üí‚Çó[ùïú] C(Œ±, Œ≤) where
  toFun := toContinuousMap
  map_smul' _ _ := rfl
  map_add' _ _ := rfl
#align bounded_continuous_function.to_continuous_map_linear_map BoundedContinuousFunction.toContinuousMapLinearMap

end Module

end BoundedSMul

section NormedSpace

/-!
### Normed space structure

In this section, if `Œ≤` is a normed space, then we show that the space of bounded
continuous functions from `Œ±` to `Œ≤` inherits a normed space structure, by using
pointwise operations and checking that they are compatible with the uniform distance. -/


variable {ùïú : Type*}
variable [TopologicalSpace Œ±] [SeminormedAddCommGroup Œ≤]
variable {f g : Œ± ‚Üí·µá Œ≤} {x : Œ±} {C : ‚Ñù}

instance instNormedSpace [NormedField ùïú] [NormedSpace ùïú Œ≤] : NormedSpace ùïú (Œ± ‚Üí·µá Œ≤) :=
  ‚ü®fun c f => by
    refine norm_ofNormedAddCommGroup_le _ (mul_nonneg (norm_nonneg _) (norm_nonneg _)) ?_
    exact fun x =>
      norm_smul c (f x) ‚ñ∏ mul_le_mul_of_nonneg_left (f.norm_coe_le_norm _) (norm_nonneg _)‚ü©

variable [NontriviallyNormedField ùïú] [NormedSpace ùïú Œ≤]
variable [SeminormedAddCommGroup Œ≥] [NormedSpace ùïú Œ≥]
variable (Œ±)

-- TODO does this work in the `BoundedSMul` setting, too?
/-- Postcomposition of bounded continuous functions into a normed module by a continuous linear map
is a continuous linear map.
Upgraded version of `ContinuousLinearMap.compLeftContinuous`, similar to `LinearMap.compLeft`. -/
protected def _root_.ContinuousLinearMap.compLeftContinuousBounded (g : Œ≤ ‚ÜíL[ùïú] Œ≥) :
    (Œ± ‚Üí·µá Œ≤) ‚ÜíL[ùïú] Œ± ‚Üí·µá Œ≥ :=
  LinearMap.mkContinuous
    { toFun := fun f =>
        ofNormedAddCommGroup (g ‚àò f) (g.continuous.comp f.continuous) (‚Äñg‚Äñ * ‚Äñf‚Äñ) fun x =>
          g.le_opNorm_of_le (f.norm_coe_le_norm x)
      map_add' := fun f g => by ext; simp
      map_smul' := fun c f => by ext; simp } ‚Äñg‚Äñ fun f =>
        norm_ofNormedAddCommGroup_le _ (mul_nonneg (norm_nonneg g) (norm_nonneg f))
          (fun x => by exact g.le_opNorm_of_le (f.norm_coe_le_norm x))
#align continuous_linear_map.comp_left_continuous_bounded ContinuousLinearMap.compLeftContinuousBounded

@[simp]
theorem _root_.ContinuousLinearMap.compLeftContinuousBounded_apply (g : Œ≤ ‚ÜíL[ùïú] Œ≥) (f : Œ± ‚Üí·µá Œ≤)
    (x : Œ±) : (g.compLeftContinuousBounded Œ± f) x = g (f x) := rfl
#align continuous_linear_map.comp_left_continuous_bounded_apply ContinuousLinearMap.compLeftContinuousBounded_apply

end NormedSpace

section NormedRing

/-!
### Normed ring structure

In this section, if `R` is a normed ring, then we show that the space of bounded
continuous functions from `Œ±` to `R` inherits a normed ring structure, by using
pointwise operations and checking that they are compatible with the uniform distance. -/


variable [TopologicalSpace Œ±] {R : Type*}

section NonUnital

section Seminormed

variable [NonUnitalSeminormedRing R]

instance instNonUnitalRing : NonUnitalRing (Œ± ‚Üí·µá R) :=
  DFunLike.coe_injective.nonUnitalRing _ coe_zero coe_add coe_mul coe_neg coe_sub
    (fun _ _ => coe_nsmul _ _) fun _ _ => coe_zsmul _ _

instance instNonUnitalSeminormedRing : NonUnitalSeminormedRing (Œ± ‚Üí·µá R) :=
  { instSeminormedAddCommGroup with
    norm_mul := fun f g =>
      norm_ofNormedAddCommGroup_le _ (mul_nonneg (norm_nonneg _) (norm_nonneg _))
        (fun x ‚Ü¶ (norm_mul_le _ _).trans <|
          mul_le_mul (norm_coe_le_norm f x) (norm_coe_le_norm g x) (norm_nonneg _) (norm_nonneg _))
    -- Porting note: These 5 fields were missing. Add them.
    left_distrib, right_distrib, zero_mul, mul_zero, mul_assoc }

end Seminormed

instance instNonUnitalNormedRing [NonUnitalNormedRing R] : NonUnitalNormedRing (Œ± ‚Üí·µá R) where
  __ := instNonUnitalSeminormedRing
  __ := instNormedAddCommGroup

end NonUnital

section Seminormed

variable [SeminormedRing R]

@[simp]
theorem coe_npowRec (f : Œ± ‚Üí·µá R) : ‚àÄ n, ‚áë(npowRec n f) = (‚áëf) ^ n
  | 0 => by rw [npowRec, pow_zero, coe_one]
  | n + 1 => by rw [npowRec, pow_succ, coe_mul, coe_npowRec f n]
#align bounded_continuous_function.coe_npow_rec BoundedContinuousFunction.coe_npowRec

instance hasNatPow : Pow (Œ± ‚Üí·µá R) ‚Ñï where
  pow f n :=
    { toContinuousMap := f.toContinuousMap ^ n
      map_bounded' := by simpa [coe_npowRec] using (npowRec n f).map_bounded' }
#align bounded_continuous_function.has_nat_pow BoundedContinuousFunction.hasNatPow

instance : NatCast (Œ± ‚Üí·µá R) :=
  ‚ü®fun n => BoundedContinuousFunction.const _ n‚ü©

@[simp, norm_cast]
theorem coe_natCast (n : ‚Ñï) : ((n : Œ± ‚Üí·µá R) : Œ± ‚Üí R) = n := rfl
#align bounded_continuous_function.coe_nat_cast BoundedContinuousFunction.coe_natCast

-- See note [no_index around OfNat.ofNat]
@[simp, norm_cast]
theorem coe_ofNat (n : ‚Ñï) [n.AtLeastTwo] :
    ((no_index OfNat.ofNat n : Œ± ‚Üí·µá R) : Œ± ‚Üí R) = OfNat.ofNat n :=
  rfl

instance : IntCast (Œ± ‚Üí·µá R) :=
  ‚ü®fun n => BoundedContinuousFunction.const _ n‚ü©

@[simp, norm_cast]
theorem coe_intCast (n : ‚Ñ§) : ((n : Œ± ‚Üí·µá R) : Œ± ‚Üí R) = n := rfl
#align bounded_continuous_function.coe_int_cast BoundedContinuousFunction.coe_intCast

instance instRing : Ring (Œ± ‚Üí·µá R) :=
  DFunLike.coe_injective.ring _ coe_zero coe_one coe_add coe_mul coe_neg coe_sub
    (fun _ _ => coe_nsmul _ _) (fun _ _ => coe_zsmul _ _) (fun _ _ => coe_pow _ _) coe_natCast
    coe_intCast

instance instSeminormedRing : SeminormedRing (Œ± ‚Üí·µá R) where
  __ := instRing
  __ := instNonUnitalSeminormedRing

end Seminormed

instance instNormedRing [NormedRing R] : NormedRing (Œ± ‚Üí·µá R) where
  __ := instRing
  __ := instNonUnitalNormedRing

end NormedRing

section NormedCommRing

/-!
### Normed commutative ring structure

In this section, if `R` is a normed commutative ring, then we show that the space of bounded
continuous functions from `Œ±` to `R` inherits a normed commutative ring structure, by using
pointwise operations and checking that they are compatible with the uniform distance. -/


variable [TopologicalSpace Œ±] {R : Type*}

instance instCommRing [SeminormedCommRing R] : CommRing (Œ± ‚Üí·µá R) where
  mul_comm _ _ := ext fun _ ‚Ü¶ mul_comm _ _

instance instSeminormedCommRing [SeminormedCommRing R] : SeminormedCommRing (Œ± ‚Üí·µá R) where
  __ := instCommRing
  __ := instSeminormedAddCommGroup
  -- Porting note (#10888): Added proof for `norm_mul`
  norm_mul := norm_mul_le

instance instNormedCommRing [NormedCommRing R] : NormedCommRing (Œ± ‚Üí·µá R) where
  __ := instCommRing
  __ := instNormedAddCommGroup
  -- Porting note (#10888): Added proof for `norm_mul`
  norm_mul := norm_mul_le

end NormedCommRing

section NormedAlgebra

/-!
### Normed algebra structure

In this section, if `Œ≥` is a normed algebra, then we show that the space of bounded
continuous functions from `Œ±` to `Œ≥` inherits a normed algebra structure, by using
pointwise operations and checking that they are compatible with the uniform distance. -/


variable {ùïú : Type*} [NormedField ùïú]
variable [TopologicalSpace Œ±] [SeminormedAddCommGroup Œ≤] [NormedSpace ùïú Œ≤]
variable [NormedRing Œ≥] [NormedAlgebra ùïú Œ≥]
variable {f g : Œ± ‚Üí·µá Œ≥} {x : Œ±} {c : ùïú}

/-- `BoundedContinuousFunction.const` as a `RingHom`. -/
def C : ùïú ‚Üí+* Œ± ‚Üí·µá Œ≥ where
  toFun := fun c : ùïú => const Œ± ((algebraMap ùïú Œ≥) c)
  map_one' := ext fun _ => (algebraMap ùïú Œ≥).map_one
  map_mul' _ _ := ext fun _ => (algebraMap ùïú Œ≥).map_mul _ _
  map_zero' := ext fun _ => (algebraMap ùïú Œ≥).map_zero
  map_add' _ _ := ext fun _ => (algebraMap ùïú Œ≥).map_add _ _
set_option linter.uppercaseLean3 false in
#align bounded_continuous_function.C BoundedContinuousFunction.C

instance instAlgebra : Algebra ùïú (Œ± ‚Üí·µá Œ≥) where
  toRingHom := C
  commutes' _ _ := ext fun _ ‚Ü¶ Algebra.commutes' _ _
  smul_def' _ _ := ext fun _ ‚Ü¶ Algebra.smul_def' _ _
#align bounded_continuous_function.algebra BoundedContinuousFunction.instAlgebra

@[simp]
theorem algebraMap_apply (k : ùïú) (a : Œ±) : algebraMap ùïú (Œ± ‚Üí·µá Œ≥) k a = k ‚Ä¢ (1 : Œ≥) := by
  simp only [Algebra.algebraMap_eq_smul_one, coe_smul, coe_one, Pi.one_apply]
#align bounded_continuous_function.algebra_map_apply BoundedContinuousFunction.algebraMap_apply

instance instNormedAlgebra : NormedAlgebra ùïú (Œ± ‚Üí·µá Œ≥) where
  __ := instAlgebra
  __ := instNormedSpace

/-!
### Structure as normed module over scalar functions

If `Œ≤` is a normed `ùïú`-space, then we show that the space of bounded continuous
functions from `Œ±` to `Œ≤` is naturally a module over the algebra of bounded continuous
functions from `Œ±` to `ùïú`. -/


instance instSMul' : SMul (Œ± ‚Üí·µá ùïú) (Œ± ‚Üí·µá Œ≤) where
  smul f g :=
    ofNormedAddCommGroup (fun x => f x ‚Ä¢ g x) (f.continuous.smul g.continuous) (‚Äñf‚Äñ * ‚Äñg‚Äñ) fun x =>
      calc
        ‚Äñf x ‚Ä¢ g x‚Äñ ‚â§ ‚Äñf x‚Äñ * ‚Äñg x‚Äñ := norm_smul_le _ _
        _ ‚â§ ‚Äñf‚Äñ * ‚Äñg‚Äñ :=
          mul_le_mul (f.norm_coe_le_norm _) (g.norm_coe_le_norm _) (norm_nonneg _) (norm_nonneg _)
#align bounded_continuous_function.has_smul' BoundedContinuousFunction.instSMul'

instance instModule' : Module (Œ± ‚Üí·µá ùïú) (Œ± ‚Üí·µá Œ≤) :=
  Module.ofMinimalAxioms
      (fun c _ _ => ext fun a => smul_add (c a) _ _)
      (fun _ _ _ => ext fun _ => add_smul _ _ _)
      (fun _ _ _ => ext fun _ => mul_smul _ _ _)
      (fun f => ext fun x => one_smul ùïú (f x))
#align bounded_continuous_function.module' BoundedContinuousFunction.instModule'

/- TODO: When `NormedModule` has been added to `Analysis.NormedSpace.Basic`, this
shows that the space of bounded continuous functions from `Œ±` to `Œ≤` is naturally a normed
module over the algebra of bounded continuous functions from `Œ±` to `ùïú`. -/
instance : BoundedSMul (Œ± ‚Üí·µá ùïú) (Œ± ‚Üí·µá Œ≤) :=
  BoundedSMul.of_norm_smul_le fun _ _ =>
    norm_ofNormedAddCommGroup_le _ (mul_nonneg (norm_nonneg _) (norm_nonneg _)) _
#align bounded_continuous_function.norm_smul_le norm_smul_le

end NormedAlgebra

theorem NNReal.upper_bound {Œ± : Type*} [TopologicalSpace Œ±] (f : Œ± ‚Üí·µá ‚Ñù‚â•0) (x : Œ±) :
    f x ‚â§ nndist f 0 := by
  have key : nndist (f x) ((0 : Œ± ‚Üí·µá ‚Ñù‚â•0) x) ‚â§ nndist f 0 := @dist_coe_le_dist Œ± ‚Ñù‚â•0 _ _ f 0 x
  simp only [coe_zero, Pi.zero_apply] at key
  rwa [NNReal.nndist_zero_eq_val' (f x)] at key
#align bounded_continuous_function.nnreal.upper_bound BoundedContinuousFunction.NNReal.upper_bound

/-!
### Star structures

In this section, if `Œ≤` is a normed ‚ãÜ-group, then so is the space of bounded
continuous functions from `Œ±` to `Œ≤`, by using the star operation pointwise.

If `ùïú` is normed field and a ‚ãÜ-ring over which `Œ≤` is a normed algebra and a
star module, then the space of bounded continuous functions from `Œ±` to `Œ≤`
is a star module.

If `Œ≤` is a ‚ãÜ-ring in addition to being a normed ‚ãÜ-group, then `Œ± ‚Üí·µá Œ≤`
inherits a ‚ãÜ-ring structure.

In summary, if `Œ≤` is a C‚ãÜ-algebra over `ùïú`, then so is `Œ± ‚Üí·µá Œ≤`; note that
completeness is guaranteed when `Œ≤` is complete (see
`BoundedContinuousFunction.complete`). -/


section NormedAddCommGroup

variable {ùïú : Type*} [NormedField ùïú] [StarRing ùïú] [TopologicalSpace Œ±] [SeminormedAddCommGroup Œ≤]
  [StarAddMonoid Œ≤] [NormedStarGroup Œ≤]

variable [NormedSpace ùïú Œ≤] [StarModule ùïú Œ≤]

instance instStarAddMonoid : StarAddMonoid (Œ± ‚Üí·µá Œ≤) where
  star f := f.comp star starNormedAddGroupHom.lipschitz
  star_involutive f := ext fun x => star_star (f x)
  star_add f g := ext fun x => star_add (f x) (g x)

/-- The right-hand side of this equality can be parsed `star ‚àò ‚áëf` because of the
instance `Pi.instStarForAll`. Upon inspecting the goal, one sees `‚ä¢ ‚Üë(star f) = star ‚Üëf`. -/
@[simp]
theorem coe_star (f : Œ± ‚Üí·µá Œ≤) : ‚áë(star f) = star (‚áëf) := rfl
#align bounded_continuous_function.coe_star BoundedContinuousFunction.coe_star

@[simp]
theorem star_apply (f : Œ± ‚Üí·µá Œ≤) (x : Œ±) : star f x = star (f x) := rfl
#align bounded_continuous_function.star_apply BoundedContinuousFunction.star_apply

instance instNormedStarGroup : NormedStarGroup (Œ± ‚Üí·µá Œ≤) where
  norm_star f := by simp only [norm_eq, star_apply, norm_star]

instance instStarModule : StarModule ùïú (Œ± ‚Üí·µá Œ≤) where
  star_smul k f := ext fun x => star_smul k (f x)

end NormedAddCommGroup

section CstarRing

variable [TopologicalSpace Œ±]
variable [NonUnitalNormedRing Œ≤] [StarRing Œ≤]

instance instStarRing [NormedStarGroup Œ≤] : StarRing (Œ± ‚Üí·µá Œ≤) where
  __ := instStarAddMonoid
  star_mul f g := ext fun x ‚Ü¶ star_mul (f x) (g x)

variable [CstarRing Œ≤]

instance instCstarRing : CstarRing (Œ± ‚Üí·µá Œ≤) where
  norm_star_mul_self {f} := by
    refine le_antisymm ?_ ?_
    ¬∑ rw [‚Üê sq, norm_le (sq_nonneg _)]
      dsimp [star_apply]
      intro x
      rw [CstarRing.norm_star_mul_self, ‚Üê sq]
      refine sq_le_sq' ?_ ?_
      ¬∑ linarith [norm_nonneg (f x), norm_nonneg f]
      ¬∑ exact norm_coe_le_norm f x
    ¬∑ rw [‚Üê sq, ‚Üê Real.le_sqrt (norm_nonneg _) (norm_nonneg _), norm_le (Real.sqrt_nonneg _)]
      intro x
      rw [Real.le_sqrt (norm_nonneg _) (norm_nonneg _), sq, ‚Üê CstarRing.norm_star_mul_self]
      exact norm_coe_le_norm (star f * f) x

end CstarRing

section NormedLatticeOrderedGroup

variable [TopologicalSpace Œ±] [NormedLatticeAddCommGroup Œ≤]

instance instPartialOrder : PartialOrder (Œ± ‚Üí·µá Œ≤) :=
  PartialOrder.lift (fun f => f.toFun) (by simp [Injective])

instance instSup : Sup (Œ± ‚Üí·µá Œ≤) where
  sup f g :=
    { toFun := f ‚äî g
      continuous_toFun := f.continuous.sup g.continuous
      map_bounded' := by
        obtain ‚ü®C‚ÇÅ, hf‚ü© := f.bounded
        obtain ‚ü®C‚ÇÇ, hg‚ü© := g.bounded
        refine ‚ü®C‚ÇÅ + C‚ÇÇ, fun x y ‚Ü¶ ?_‚ü©
        simp_rw [NormedAddCommGroup.dist_eq] at hf hg ‚ä¢
        exact (norm_sup_sub_sup_le_add_norm _ _ _ _).trans (add_le_add (hf _ _) (hg _ _)) }

instance instInf : Inf (Œ± ‚Üí·µá Œ≤) where
  inf f g :=
    { toFun := f ‚äì g
      continuous_toFun := f.continuous.inf g.continuous
      map_bounded' := by
        obtain ‚ü®C‚ÇÅ, hf‚ü© := f.bounded
        obtain ‚ü®C‚ÇÇ, hg‚ü© := g.bounded
        refine ‚ü®C‚ÇÅ + C‚ÇÇ, fun x y ‚Ü¶ ?_‚ü©
        simp_rw [NormedAddCommGroup.dist_eq] at hf hg ‚ä¢
        exact (norm_inf_sub_inf_le_add_norm _ _ _ _).trans (add_le_add (hf _ _) (hg _ _)) }

@[simp, norm_cast] lemma coe_sup (f g : Œ± ‚Üí·µá Œ≤) : ‚áë(f ‚äî g) = ‚áëf ‚äî ‚áëg := rfl
#align bounded_continuous_function.coe_fn_sup BoundedContinuousFunction.coe_sup

@[simp, norm_cast] lemma coe_inf (f g : Œ± ‚Üí·µá Œ≤) : ‚áë(f ‚äì g) = ‚áëf ‚äì ‚áëg := rfl

instance instSemilatticeSup : SemilatticeSup (Œ± ‚Üí·µá Œ≤) :=
  DFunLike.coe_injective.semilatticeSup _ coe_sup

instance instSemilatticeInf : SemilatticeInf (Œ± ‚Üí·µá Œ≤) :=
  DFunLike.coe_injective.semilatticeInf _ coe_inf

instance instLattice : Lattice (Œ± ‚Üí·µá Œ≤) := DFunLike.coe_injective.lattice _ coe_sup coe_inf

@[simp, norm_cast] lemma coe_abs (f : Œ± ‚Üí·µá Œ≤) : ‚áë|f| = |‚áëf| := rfl
#align bounded_continuous_function.coe_fn_abs BoundedContinuousFunction.coe_abs

@[simp, norm_cast] lemma coe_posPart (f : Œ± ‚Üí·µá Œ≤) : ‚áëf‚Å∫ = (‚áëf)‚Å∫ := rfl
@[simp, norm_cast] lemma coe_negPart (f : Œ± ‚Üí·µá Œ≤) : ‚áëf‚Åª = (‚áëf)‚Åª := rfl

-- 2024-02-21
@[deprecated] alias coeFn_sup := coe_sup
@[deprecated] alias coeFn_abs := coe_abs

instance instNormedLatticeAddCommGroup : NormedLatticeAddCommGroup (Œ± ‚Üí·µá Œ≤) :=
  { instSeminormedAddCommGroup with
    add_le_add_left := by
      intro f g h‚ÇÅ h t
      simp only [coe_to_continuous_fun, Pi.add_apply, add_le_add_iff_left, coe_add,
        ContinuousMap.toFun_eq_coe]
      exact h‚ÇÅ _
    solid := by
      intro f g h
      have i1 : ‚àÄ t, ‚Äñf t‚Äñ ‚â§ ‚Äñg t‚Äñ := fun t => HasSolidNorm.solid (h t)
      rw [norm_le (norm_nonneg _)]
      exact fun t => (i1 t).trans (norm_coe_le_norm g t)
    -- Porting note (#10888): added proof for `eq_of_dist_eq_zero`
    eq_of_dist_eq_zero }

end NormedLatticeOrderedGroup

section NonnegativePart

variable [TopologicalSpace Œ±]

/-- The nonnegative part of a bounded continuous `‚Ñù`-valued function as a bounded
continuous `‚Ñù‚â•0`-valued function. -/
def nnrealPart (f : Œ± ‚Üí·µá ‚Ñù) : Œ± ‚Üí·µá ‚Ñù‚â•0 :=
  BoundedContinuousFunction.comp _ (show LipschitzWith 1 Real.toNNReal from lipschitzWith_posPart) f
#align bounded_continuous_function.nnreal_part BoundedContinuousFunction.nnrealPart

@[simp]
theorem nnrealPart_coeFn_eq (f : Œ± ‚Üí·µá ‚Ñù) : ‚áëf.nnrealPart = Real.toNNReal ‚àò ‚áëf := rfl
#align bounded_continuous_function.nnreal_part_coe_fun_eq BoundedContinuousFunction.nnrealPart_coeFn_eq

/-- The absolute value of a bounded continuous `‚Ñù`-valued function as a bounded
continuous `‚Ñù‚â•0`-valued function. -/
def nnnorm (f : Œ± ‚Üí·µá ‚Ñù) : Œ± ‚Üí·µá ‚Ñù‚â•0 :=
  BoundedContinuousFunction.comp _
    (show LipschitzWith 1 fun x : ‚Ñù => ‚Äñx‚Äñ‚Çä from lipschitzWith_one_norm) f
#align bounded_continuous_function.nnnorm BoundedContinuousFunction.nnnorm

@[simp]
theorem nnnorm_coeFn_eq (f : Œ± ‚Üí·µá ‚Ñù) : ‚áëf.nnnorm = NNNorm.nnnorm ‚àò ‚áëf := rfl
#align bounded_continuous_function.nnnorm_coe_fun_eq BoundedContinuousFunction.nnnorm_coeFn_eq

-- TODO: Use `posPart` and `negPart` here
/-- Decompose a bounded continuous function to its positive and negative parts. -/
theorem self_eq_nnrealPart_sub_nnrealPart_neg (f : Œ± ‚Üí·µá ‚Ñù) :
    ‚áëf = (‚Üë) ‚àò f.nnrealPart - (‚Üë) ‚àò (-f).nnrealPart := by
  funext x
  dsimp
  simp only [max_zero_sub_max_neg_zero_eq_self]
#align bounded_continuous_function.self_eq_nnreal_part_sub_nnreal_part_neg BoundedContinuousFunction.self_eq_nnrealPart_sub_nnrealPart_neg

/-- Express the absolute value of a bounded continuous function in terms of its
positive and negative parts. -/
theorem abs_self_eq_nnrealPart_add_nnrealPart_neg (f : Œ± ‚Üí·µá ‚Ñù) :
    abs ‚àò ‚áëf = (‚Üë) ‚àò f.nnrealPart + (‚Üë) ‚àò (-f).nnrealPart := by
  funext x
  dsimp
  simp only [max_zero_add_max_neg_zero_eq_abs_self]
#align bounded_continuous_function.abs_self_eq_nnreal_part_add_nnreal_part_neg BoundedContinuousFunction.abs_self_eq_nnrealPart_add_nnrealPart_neg

end NonnegativePart

section

variable {Œ± : Type*} [TopologicalSpace Œ±]

-- TODO: `f + const _ ‚Äñf‚Äñ` is just `f‚Å∫`
lemma add_norm_nonneg (f : Œ± ‚Üí·µá ‚Ñù) :
    0 ‚â§ f + const _ ‚Äñf‚Äñ := by
  intro x
  dsimp
  linarith [(abs_le.mp (norm_coe_le_norm f x)).1]

lemma norm_sub_nonneg (f : Œ± ‚Üí·µá ‚Ñù) :
    0 ‚â§ const _ ‚Äñf‚Äñ - f := by
  intro x
  dsimp
  linarith [(abs_le.mp (norm_coe_le_norm f x)).2]

end

end BoundedContinuousFunction
