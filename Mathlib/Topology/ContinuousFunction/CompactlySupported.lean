/-
Copyright (c) 2024 Yoh Tanimoto. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yoh Tanimoto
-/
import Mathlib.Topology.ContinuousFunction.CocompactMap
import Mathlib.Topology.ContinuousFunction.ZeroAtInfty
import Mathlib.Topology.Support

/-!
# Compactly supported continuous functions

In this file, we define the type `C_c(Î±, Î²)` of compactly supported continuous functions and the
class `CompactlySupportedContinuousMapClass`, and prove basic properties.

## Main definitions and results

This file contains various instances such as `Add`, `Mul`, `SMul F C_c(Î±, Î²)` when `F` is a class of
continuous functions.
When `Î²` has more structures, `C_c(Î±, Î²)` inherits such structures as `AddCommGroup`,
`NonUnitalRing` and `StarRing`.

When the domain `Î±` is compact, `ContinuousMap.liftCompactlySupported` gives the identification
`C(Î±, Î²) â‰ƒ C_c(Î±, Î²)`.

-/

variable {F Î± Î² Î³ : Type*} [TopologicalSpace Î±]

open Topology

open Filter Metric

/-- `C_c(Î±, Î²)` is the type of continuous functions `Î± â†’ Î²` with compact support from a topological
space to a topological space with a zero element.

When possible, instead of parametrizing results over `f : C_c(Î±, Î²)`,
you should parametrize over `{F : Type*} [CompactlySupportedContinuousMapClass F Î± Î²] (f : F)`.

When you extend this structure, make sure to extend `CompactlySupportedContinuousMapClass`. -/
structure CompactlySupportedContinuousMap (Î± Î² : Type*) [TopologicalSpace Î±] [Zero Î²]
    [TopologicalSpace Î²] extends ContinuousMap Î± Î² where
  /-- The function has compact support . -/
  hasCompactSupport' : HasCompactSupport toFun

@[inherit_doc]
scoped[CompactlySupported] notation (priority := 2000)
  "C_c(" Î± ", " Î² ")" => CompactlySupportedContinuousMap Î± Î²

@[inherit_doc]
scoped[CompactlySupported] notation Î± " â†’C_c " Î² => CompactlySupportedContinuousMap Î± Î²

open CompactlySupported

section

/-- `CompactlySupportedContinuousMapClass F Î± Î²` states that `F` is a type of continuous maps with
compact support.

You should also extend this typeclass when you extend `CompactlySupportedContinuousMap`. -/
class CompactlySupportedContinuousMapClass (F : Type*) (Î± Î² : outParam <| Type*)
    [TopologicalSpace Î±] [Zero Î²] [TopologicalSpace Î²] [FunLike F Î± Î²]
    extends ContinuousMapClass F Î± Î² : Prop where
  /-- Each member of the class has compact support. -/
  hasCompactSupport (f : F) : HasCompactSupport f

end

namespace CompactlySupportedContinuousMap

section Basics

variable [TopologicalSpace Î²] [Zero Î²]

instance : FunLike C_c(Î±, Î²) Î± Î² where
  coe f := f.toFun
  coe_injective' f g h := by
    obtain âŸ¨âŸ¨_, _âŸ©, _âŸ© := f
    obtain âŸ¨âŸ¨_, _âŸ©, _âŸ© := g
    congr

protected lemma hasCompactSupport (f : C_c(Î±, Î²)) : HasCompactSupport f := f.hasCompactSupport'

instance : CompactlySupportedContinuousMapClass C_c(Î±, Î²) Î± Î² where
  map_continuous f := f.continuous_toFun
  hasCompactSupport f := f.hasCompactSupport'

@[simp]
theorem coe_toContinuousMap (f : C_c(Î±, Î²)) : (f.toContinuousMap : Î± â†’ Î²) = f :=
  rfl

@[ext]
theorem ext {f g : C_c(Î±, Î²)} (h : âˆ€ x, f x = g x) : f = g :=
  DFunLike.ext _ _ h

@[simp]
theorem coe_mk (f : C(Î±, Î²)) (h : HasCompactSupport f) : â‡‘(âŸ¨f, hâŸ© : C_c(Î±, Î²)) = f :=
  rfl

/-- Copy of a `CompactlySupportedContinuousMap` with a new `toFun` equal to the old one. Useful
to fix definitional equalities. -/
protected def copy (f : C_c(Î±, Î²)) (f' : Î± â†’ Î²) (h : f' = f) : C_c(Î±, Î²) where
  toFun := f'
  continuous_toFun := by
    rw [h]
    exact f.continuous_toFun
  hasCompactSupport' := by
    simp_rw [h]
    exact f.hasCompactSupport'

@[simp]
theorem coe_copy (f : C_c(Î±, Î²)) (f' : Î± â†’ Î²) (h : f' = f) : â‡‘(f.copy f' h) = f' :=
  rfl

theorem copy_eq (f : C_c(Î±, Î²)) (f' : Î± â†’ Î²) (h : f' = f) : f.copy f' h = f :=
  DFunLike.ext' h

theorem eq_of_empty [IsEmpty Î±] (f g : C_c(Î±, Î²)) : f = g :=
  ext <| IsEmpty.elim â€¹_â€º

/-- A continuous function on a compact space automatically has compact support. -/
@[simps]
def ContinuousMap.liftCompactlySupported [CompactSpace Î±] : C(Î±, Î²) â‰ƒ C_c(Î±, Î²) where
  toFun f :=
    { toFun := f
      continuous_toFun := f.continuous
      hasCompactSupport' := HasCompactSupport.of_compactSpace f }
  invFun f := f
  left_inv _ := rfl
  right_inv _ := rfl

end Basics

/-! ### Algebraic structure

Whenever `Î²` has the structure of continuous additive monoid and a compatible topological structure,
then `C_c(Î±, Î²)` inherits a corresponding algebraic structure. The primary exception to this is that
`C_c(Î±, Î²)` will not have a multiplicative identity.
-/

section AlgebraicStructure

variable [TopologicalSpace Î²] (x : Î±)

instance [Zero Î²] : Zero C_c(Î±, Î²) where
  zero := { toFun := (0 : C(Î±, Î²))
            continuous_toFun := (0 : C(Î±, Î²)).2
            hasCompactSupport' := by simp [HasCompactSupport, tsupport] }

instance [Zero Î²] : Inhabited C_c(Î±, Î²) :=
  âŸ¨0âŸ©

@[simp]
theorem coe_zero [Zero Î²] : â‡‘(0 : C_c(Î±, Î²)) = 0 :=
  rfl

theorem zero_apply [Zero Î²] : (0 : C_c(Î±, Î²)) x = 0 :=
  rfl

instance [MulZeroClass Î²] [ContinuousMul Î²] : Mul C_c(Î±, Î²) :=
  âŸ¨fun f g => âŸ¨f * g, HasCompactSupport.mul_left g.2âŸ©âŸ©

@[simp]
theorem coe_mul [MulZeroClass Î²] [ContinuousMul Î²] (f g : C_c(Î±, Î²)) : â‡‘(f * g) = f * g :=
  rfl

theorem mul_apply [MulZeroClass Î²] [ContinuousMul Î²] (f g : C_c(Î±, Î²)) : (f * g) x = f x * g x :=
  rfl

/-- the product of `f : F` assuming `ContinuousMapClass F Î± Î³` and `ContinuousSMul Î³ Î²` and
`g : C_c(Î±, Î²)` is in `C_c(Î±, Î²)` -/
instance [Zero Î²] [TopologicalSpace Î³] [SMulZeroClass Î³ Î²] [ContinuousSMul Î³ Î²]
    {F : Type*} [FunLike F Î± Î³] [ContinuousMapClass F Î± Î³] : SMul F C_c(Î±, Î²) where
  smul f g :=
    âŸ¨âŸ¨fun x â†¦ f x â€¢ g x, (map_continuous f).smul g.continuousâŸ©, g.hasCompactSupport'.smul_leftâŸ©

@[simp]
theorem coe_smulc [Zero Î²] [TopologicalSpace Î³] [SMulZeroClass Î³ Î²] [ContinuousSMul Î³ Î²]
    {F : Type*} [FunLike F Î± Î³] [ContinuousMapClass F Î± Î³] (f : F) (g : C_c(Î±, Î²)) :
    â‡‘(f â€¢ g) = fun x => f x â€¢ g x :=
  rfl

theorem smulc_apply [Zero Î²] [TopologicalSpace Î³] [SMulZeroClass Î³ Î²] [ContinuousSMul Î³ Î²]
    {F : Type*} [FunLike F Î± Î³] [ContinuousMapClass F Î± Î³] (f : F) (g : C_c(Î±, Î²)) (x : Î±) :
    (f â€¢ g) x = f x â€¢ g x :=
  rfl

instance [MulZeroClass Î²] [ContinuousMul Î²] : MulZeroClass C_c(Î±, Î²) :=
  DFunLike.coe_injective.mulZeroClass _ coe_zero coe_mul

instance [SemigroupWithZero Î²] [ContinuousMul Î²] :
    SemigroupWithZero C_c(Î±, Î²) :=
  DFunLike.coe_injective.semigroupWithZero _ coe_zero coe_mul

instance [AddZeroClass Î²] [ContinuousAdd Î²] : Add C_c(Î±, Î²) :=
  âŸ¨fun f g => âŸ¨f + g, HasCompactSupport.add f.2 g.2âŸ©âŸ©

@[simp]
theorem coe_add [AddZeroClass Î²] [ContinuousAdd Î²] (f g : C_c(Î±, Î²)) : â‡‘(f + g) = f + g :=
  rfl

theorem add_apply [AddZeroClass Î²] [ContinuousAdd Î²] (f g : C_c(Î±, Î²)) : (f + g) x = f x + g x :=
  rfl

instance [AddZeroClass Î²] [ContinuousAdd Î²] : AddZeroClass C_c(Î±, Î²) :=
  DFunLike.coe_injective.addZeroClass _ coe_zero coe_add

/-- Coercion to a function as a `AddMonoidHom`. Similar to `AddMonoidHom.coeFn`. -/
def coeFnMonoidHom [AddMonoid Î²] [ContinuousAdd Î²] : C_c(Î±, Î²) â†’+ Î± â†’ Î² where
  toFun f := f
  map_zero' := coe_zero
  map_add' := coe_add

instance [Zero Î²] {R : Type*} [SMulZeroClass R Î²] [ContinuousConstSMul R Î²] :
    SMul R C_c(Î±, Î²) :=
  âŸ¨fun r f => âŸ¨âŸ¨r â€¢ â‡‘f, Continuous.const_smul f.continuous râŸ©, HasCompactSupport.smul_left f.2âŸ©âŸ©

@[simp, norm_cast]
theorem coe_smul [Zero Î²] {R : Type*} [SMulZeroClass R Î²] [ContinuousConstSMul R Î²] (r : R)
    (f : C_c(Î±, Î²)) : â‡‘(r â€¢ f) = r â€¢ â‡‘f :=
  rfl

theorem smul_apply [Zero Î²] {R : Type*} [SMulZeroClass R Î²] [ContinuousConstSMul R Î²] (r : R)
    (f : C_c(Î±, Î²)) (x : Î±) : (r â€¢ f) x = r â€¢ f x :=
  rfl

section AddMonoid

instance [AddMonoid Î²] [ContinuousAdd Î²] : AddMonoid C_c(Î±, Î²) :=
  DFunLike.coe_injective.addMonoid _ coe_zero coe_add fun _ _ => rfl

end AddMonoid

instance [AddCommMonoid Î²] [ContinuousAdd Î²] : AddCommMonoid C_c(Î±, Î²) :=
  DFunLike.coe_injective.addCommMonoid _ coe_zero coe_add fun _ _ => rfl

open BigOperators

@[simp]
theorem coe_sum [AddCommMonoid Î²] [ContinuousAdd Î²] {Î¹ : Type*} (s : Finset Î¹) (f : Î¹ â†’ C_c(Î±, Î²)) :
    â‡‘(âˆ‘ i in s, f i) = âˆ‘ i in s, (f i : Î± â†’ Î²) :=
  map_sum coeFnMonoidHom f s

theorem sum_apply [AddCommMonoid Î²] [ContinuousAdd Î²] {Î¹ : Type*} (s : Finset Î¹) (f : Î¹ â†’ C_c(Î±, Î²))
    (a : Î±) : (âˆ‘ i in s, f i) a = âˆ‘ i in s, f i a := by simp

section AddGroup

variable [AddGroup Î²] [TopologicalAddGroup Î²] (f g : C_c(Î±, Î²))

instance : Neg C_c(Î±, Î²) where
  neg f := { toFun := -f.1
             continuous_toFun := map_continuous (-f.1)
             hasCompactSupport' := by simpa [HasCompactSupport, tsupport] using f.2 }

@[simp]
theorem coe_neg : â‡‘(-f) = -f :=
  rfl

theorem neg_apply : (-f) x = -f x :=
  rfl

instance : Sub C_c(Î±, Î²) where
  sub f g := { toFun := f.1 - g.1
               continuous_toFun := map_continuous (f.1 - g.1)
               hasCompactSupport' := by
                 simpa [sub_eq_add_neg] using HasCompactSupport.add f.2 (-g).2 }

@[simp]
theorem coe_sub : â‡‘(f - g) = f - g :=
  rfl

theorem sub_apply : (f - g) x = f x - g x :=
  rfl

instance : AddGroup C_c(Î±, Î²) :=
  DFunLike.coe_injective.addGroup _ coe_zero coe_add coe_neg coe_sub (fun _ _ => rfl) fun _ _ => rfl

end AddGroup

instance [AddCommGroup Î²] [TopologicalAddGroup Î²] : AddCommGroup C_c(Î±, Î²) :=
  DFunLike.coe_injective.addCommGroup _ coe_zero coe_add coe_neg coe_sub (fun _ _ => rfl) fun _ _ =>
    rfl

instance [Zero Î²] {R : Type*} [Zero R] [SMulWithZero R Î²] [SMulWithZero Ráµáµ’áµ– Î²]
    [ContinuousConstSMul R Î²] [IsCentralScalar R Î²] : IsCentralScalar R C_c(Î±, Î²) :=
  âŸ¨fun _ _ => ext fun _ => op_smul_eq_smul _ _âŸ©

instance [Zero Î²] {R : Type*} [Zero R] [SMulWithZero R Î²]
    [ContinuousConstSMul R Î²] : SMulWithZero R C_c(Î±, Î²) :=
  Function.Injective.smulWithZero âŸ¨_, coe_zeroâŸ© DFunLike.coe_injective coe_smul

instance [Zero Î²] {R : Type*} [MonoidWithZero R] [MulActionWithZero R Î²]
    [ContinuousConstSMul R Î²] : MulActionWithZero R C_c(Î±, Î²) :=
  Function.Injective.mulActionWithZero âŸ¨_, coe_zeroâŸ© DFunLike.coe_injective coe_smul

instance [AddCommMonoid Î²] [ContinuousAdd Î²] {R : Type*} [Semiring R] [Module R Î²]
    [ContinuousConstSMul R Î²] : Module R C_c(Î±, Î²) :=
  Function.Injective.module R âŸ¨âŸ¨_, coe_zeroâŸ©, coe_addâŸ© DFunLike.coe_injective coe_smul

instance [NonUnitalNonAssocSemiring Î²] [TopologicalSemiring Î²] :
    NonUnitalNonAssocSemiring C_c(Î±, Î²) :=
  DFunLike.coe_injective.nonUnitalNonAssocSemiring _ coe_zero coe_add coe_mul fun _ _ => rfl

instance [NonUnitalSemiring Î²] [TopologicalSemiring Î²] :
    NonUnitalSemiring C_c(Î±, Î²) :=
  DFunLike.coe_injective.nonUnitalSemiring _ coe_zero coe_add coe_mul fun _ _ => rfl

instance [NonUnitalCommSemiring Î²] [TopologicalSemiring Î²] :
    NonUnitalCommSemiring C_c(Î±, Î²) :=
  DFunLike.coe_injective.nonUnitalCommSemiring _ coe_zero coe_add coe_mul fun _ _ => rfl

instance [NonUnitalNonAssocRing Î²] [TopologicalRing Î²] :
    NonUnitalNonAssocRing C_c(Î±, Î²) :=
  DFunLike.coe_injective.nonUnitalNonAssocRing _ coe_zero coe_add coe_mul coe_neg coe_sub
    (fun _ _ => rfl) fun _ _ => rfl

instance [NonUnitalRing Î²] [TopologicalRing Î²] : NonUnitalRing C_c(Î±, Î²) :=
  DFunLike.coe_injective.nonUnitalRing _ coe_zero coe_add coe_mul coe_neg coe_sub (fun _ _ => rfl)
    fun _ _ => rfl

instance [NonUnitalCommRing Î²] [TopologicalRing Î²] :
    NonUnitalCommRing C_c(Î±, Î²) :=
  DFunLike.coe_injective.nonUnitalCommRing _ coe_zero coe_add coe_mul coe_neg coe_sub
    (fun _ _ => rfl) fun _ _ => rfl

instance {R : Type*} [Semiring R] [NonUnitalNonAssocSemiring Î²]
    [TopologicalSemiring Î²] [Module R Î²] [ContinuousConstSMul R Î²] [IsScalarTower R Î² Î²] :
    IsScalarTower R C_c(Î±, Î²) C_c(Î±, Î²) where
  smul_assoc r f g := by
    ext
    simp only [smul_eq_mul, coe_mul, coe_smul, Pi.mul_apply, Pi.smul_apply]
    rw [â† smul_eq_mul, â† smul_eq_mul, smul_assoc]

instance {R : Type*} [Semiring R] [NonUnitalNonAssocSemiring Î²]
    [TopologicalSemiring Î²] [Module R Î²] [ContinuousConstSMul R Î²] [SMulCommClass R Î² Î²] :
    SMulCommClass R C_c(Î±, Î²) C_c(Î±, Î²) where
  smul_comm r f g := by
    ext
    simp only [smul_eq_mul, coe_smul, coe_mul, Pi.smul_apply, Pi.mul_apply]
    rw [â† smul_eq_mul, â† smul_eq_mul, smul_comm]

end AlgebraicStructure

section Star

/-! ### Star structure

It is possible to equip `C_c(Î±, Î²)` with a pointwise `star` operation whenever there is a continuous
`star : Î² â†’ Î²` for which `star (0 : Î²) = 0`. We don't have quite this weak a typeclass, but
`StarAddMonoid` is close enough.

The `StarAddMonoid` class on `C_c(Î±, Î²)` is inherited from their counterparts on `Î± â†’áµ‡ Î²`.
-/


variable [TopologicalSpace Î²] [AddMonoid Î²] [StarAddMonoid Î²] [ContinuousStar Î²]

instance : Star C_c(Î±, Î²) where
  star f :=
    { toFun := fun x => star (f x)
      continuous_toFun := (map_continuous f).star
      hasCompactSupport' := by
        rw [HasCompactSupport, tsupport]
        simp only
        have support_star : (Function.support fun (x : Î±) => star (f x)) = Function.support f := by
          ext x
          simp only [Function.mem_support, ne_eq, star_eq_zero]
        rw [support_star]
        exact f.2 }

@[simp]
theorem coe_star (f : C_c(Î±, Î²)) : â‡‘(star f) = star (â‡‘f) :=
  rfl

theorem star_apply (f : C_c(Î±, Î²)) (x : Î±) : (star f) x = star (f x) :=
  rfl

instance [TrivialStar Î²] : TrivialStar C_c(Î±, Î²) where
    star_trivial f := ext fun x => star_trivial (f x)

instance [ContinuousAdd Î²] : StarAddMonoid C_c(Î±, Î²) where
  star_involutive f := ext fun x => star_star (f x)
  star_add f g := ext fun x => star_add (f x) (g x)

end Star

section StarModule

variable {ð•œ : Type*} [Zero ð•œ] [Star ð•œ] [AddMonoid Î²] [StarAddMonoid Î²] [TopologicalSpace Î²]
  [ContinuousStar Î²] [SMulWithZero ð•œ Î²] [ContinuousConstSMul ð•œ Î²] [StarModule ð•œ Î²]

instance : StarModule ð•œ C_c(Î±, Î²) where
  star_smul k f := ext fun x => star_smul k (f x)

end StarModule

section StarRing

variable [NonUnitalSemiring Î²] [StarRing Î²] [TopologicalSpace Î²] [ContinuousStar Î²]
  [TopologicalSemiring Î²]

instance : StarRing C_c(Î±, Î²) :=
  { CompactlySupportedContinuousMap.instStarAddMonoid with
    star_mul := fun f g => ext fun x => star_mul (f x) (g x) }

end StarRing

/-! ### `C_c` as a functor

For each `Î²` with sufficient structure, there is a contravariant functor `C_c(-, Î²)` from the
category of topological spaces with morphisms given by `CocompactMap`s.
-/


variable {Î´ : Type*} [TopologicalSpace Î²] [TopologicalSpace Î³] [TopologicalSpace Î´]

local notation Î± " â†’co " Î² => CocompactMap Î± Î²

section

variable [Zero Î´]

/-- Composition of a continuous function with compact support with a cocompact map
yields another continuous function with compact support. -/
def comp (f : C_c(Î³, Î´)) (g : Î² â†’co Î³) : C_c(Î², Î´) where
  toContinuousMap := (f : C(Î³, Î´)).comp g
  hasCompactSupport' := by
    apply IsCompact.of_isClosed_subset (g.isCompact_preimage_of_isClosed f.2 (isClosed_tsupport _))
      (isClosed_tsupport (f âˆ˜ g))
    intro x hx
    rw [tsupport, Set.mem_preimage, _root_.mem_closure_iff]
    intro o ho hgxo
    rw [tsupport, _root_.mem_closure_iff] at hx
    obtain âŸ¨y, hyâŸ© := hx (g â»Â¹' o) (IsOpen.preimage g.1.2 ho) hgxo
    exact âŸ¨g y, hyâŸ©

@[simp]
theorem coe_comp_to_continuous_fun (f : C_c(Î³, Î´)) (g : Î² â†’co Î³) : ((f.comp g) : Î² â†’ Î´) = f âˆ˜ g :=
  rfl

@[simp]
theorem comp_id (f : C_c(Î³, Î´)) : f.comp (CocompactMap.id Î³) = f :=
  ext fun _ => rfl

variable [T2Space Î²]

@[simp]
theorem comp_assoc (f : C_c(Î³, Î´)) (g : Î² â†’co Î³) (h : Î± â†’co Î²) :
    (f.comp g).comp h = f.comp (g.comp h) :=
  rfl

@[simp]
theorem zero_comp (g : Î² â†’co Î³) : (0 : C_c(Î³, Î´)).comp g = 0 :=
  rfl

end

variable [T2Space Î³]

/-- Composition as an additive monoid homomorphism. -/
def compAddMonoidHom [AddMonoid Î´] [ContinuousAdd Î´] (g : Î² â†’co Î³) : C_c(Î³, Î´) â†’+ C_c(Î², Î´) where
  toFun f := f.comp g
  map_zero' := zero_comp g
  map_add' _ _ := rfl

/-- Composition as a semigroup homomorphism. -/
def compMulHom [MulZeroClass Î´] [ContinuousMul Î´] (g : Î² â†’co Î³) : C_c(Î³, Î´) â†’â‚™* C_c(Î², Î´) where
  toFun f := f.comp g
  map_mul' _ _ := rfl

/-- Composition as a linear map. -/
def compLinearMap [AddCommMonoid Î´] [ContinuousAdd Î´] {R : Type*} [Semiring R] [Module R Î´]
    [ContinuousConstSMul R Î´] (g : Î² â†’co Î³) : C_c(Î³, Î´) â†’â‚—[R] C_c(Î², Î´) where
  toFun f := f.comp g
  map_add' _ _ := rfl
  map_smul' _ _ := rfl

/-- Composition as a non-unital algebra homomorphism. -/
def compNonUnitalAlgHom {R : Type*} [Semiring R] [NonUnitalNonAssocSemiring Î´]
    [TopologicalSemiring Î´] [Module R Î´] [ContinuousConstSMul R Î´] (g : Î² â†’co Î³) :
    C_c(Î³, Î´) â†’â‚™â‚[R] C_c(Î², Î´) where
  toFun f := f.comp g
  map_smul' _ _ := rfl
  map_zero' := rfl
  map_add' _ _ := rfl
  map_mul' _ _ := rfl

end CompactlySupportedContinuousMap

namespace CompactlySupportedContinuousMapClass

section Basic

variable [Zero Î²] [TopologicalSpace Î²] [FunLike F Î± Î²] [CompactlySupportedContinuousMapClass F Î± Î²]

instance : CoeTC F (CompactlySupportedContinuousMap Î± Î²) :=
  âŸ¨fun f =>
    { toFun := f
      continuous_toFun := map_continuous f
      hasCompactSupport' := hasCompactSupport f }âŸ©

/-- A continuous function on a compact space has automatically compact support. This is not an
instance to avoid type class loops. -/
lemma of_compactSpace (G : Type*) [FunLike G Î± Î²]
    [ContinuousMapClass G Î± Î²] [CompactSpace Î±] : CompactlySupportedContinuousMapClass G Î± Î² where
  map_continuous := map_continuous
  hasCompactSupport := by
    intro f
    exact HasCompactSupport.of_compactSpace f

end Basic

section Uniform

variable [UniformSpace Î²] [UniformSpace Î³] [Zero Î³] [FunLike F Î² Î³]
  [CompactlySupportedContinuousMapClass F Î² Î³]

theorem uniformContinuous (f : F) : UniformContinuous (f : Î² â†’ Î³) :=
  (map_continuous f).uniformContinuous_of_tendsto_cocompact
  (HasCompactSupport.is_zero_at_infty (hasCompactSupport f))

end Uniform

section ZeroAtInfty

open ZeroAtInfty

variable [TopologicalSpace Î²] [TopologicalSpace Î³] [Zero Î³]
  [FunLike F Î² Î³] [CompactlySupportedContinuousMapClass F Î² Î³]

instance : ZeroAtInftyContinuousMapClass F Î² Î³ where
  zero_at_infty f := HasCompactSupport.is_zero_at_infty (hasCompactSupport f)

end ZeroAtInfty

end CompactlySupportedContinuousMapClass
