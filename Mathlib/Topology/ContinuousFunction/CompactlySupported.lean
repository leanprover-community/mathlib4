/-
Copyright (c) 2024 Yoh Tanimoto. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yoh Tanimoto
-/
import Mathlib.Topology.ContinuousFunction.Bounded
import Mathlib.Topology.ContinuousFunction.CocompactMap
import Mathlib.Topology.ContinuousFunction.ZeroAtInfty

/-!
# Compactly supported continuous functions

The type of compactly supported continuous functions. When the domain is compact,
`C(Œ±, Œ≤) ‚âÉ C_c(Œ±, Œ≤)` via the identity map. When the codomain is a metric space, every continuous
compactly supported map is a bounded continuous function. When the domain is a locally
compact space, this type has nice properties.

## TODO

* Create more intances of algebraic structures (e.g., `NonUnitalSemiring`) once the necessary
  type classes (e.g., `TopologicalRing`) are sufficiently generalized.
-/


universe u v w

variable {F : Type*} {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w} [TopologicalSpace Œ±]

open BoundedContinuousFunction Topology Bornology

open Filter Metric

/-- `C_c(Œ±, Œ≤)` is the type of continuous functions `Œ± ‚Üí Œ≤` with compact support from a topological
space to a metric space with a zero element.

When possible, instead of parametrizing results over `(f : C_c(Œ±, Œ≤))`,
you should parametrize over `(F : Type*) [CompactlySupportedContinuousMapClass F Œ± Œ≤] (f : F)`.

When you extend this structure, make sure to extend `CompactlySupportedContinuousMapClass`. -/
structure CompactlySupportedContinuousMap (Œ± : Type u) (Œ≤ : Type v) [TopologicalSpace Œ±] [Zero Œ≤]
    [TopologicalSpace Œ≤] extends ContinuousMap Œ± Œ≤ : Type max u v where
  /-- The function has compact support . -/
  has_compact_support' : HasCompactSupport toFun

@[inherit_doc]
scoped[CompactlySupported] notation (priority := 2000)
  "C_c(" Œ± ", " Œ≤ ")" => CompactlySupportedContinuousMap Œ± Œ≤

@[inherit_doc]
scoped[CompactlySupported] notation Œ± " ‚ÜíC_c " Œ≤ => CompactlySupportedContinuousMap Œ± Œ≤

open CompactlySupported

section

/-- `CompactlySupportedContinuousMapClass F Œ± Œ≤` states that `F` is a type of continuous maps with
compact support.

You should also extend this typeclass when you extend `CompactlySupportedContinuousMap`. -/
class CompactlySupportedContinuousMapClass (F : Type*) (Œ± Œ≤ : outParam <| Type*)
    [TopologicalSpace Œ±] [Zero Œ≤] [TopologicalSpace Œ≤] [FunLike F Œ± Œ≤]
    extends ContinuousMapClass F Œ± Œ≤ : Prop where
  /-- Each member of the class has compact support. -/
  has_compact_support (f : F) : HasCompactSupport f

end

export CompactlySupportedContinuousMapClass (has_compact_support)

namespace CompactlySupportedContinuousMap

section Basics

variable [TopologicalSpace Œ≤] [Zero Œ≤] [FunLike F Œ± Œ≤] [CompactlySupportedContinuousMapClass F Œ± Œ≤]

instance instFunLike : FunLike C_c(Œ±, Œ≤) Œ± Œ≤ where
  coe f := f.toFun
  coe_injective' f g h := by
    obtain ‚ü®‚ü®_, _‚ü©, _‚ü© := f
    obtain ‚ü®‚ü®_, _‚ü©, _‚ü© := g
    congr

instance instCompactlySupportedContinuousMapClass :
    CompactlySupportedContinuousMapClass C_c(Œ±, Œ≤) Œ± Œ≤ where
  map_continuous f := f.continuous_toFun
  has_compact_support f := f.has_compact_support'

instance instCoeTC : CoeTC F C_c(Œ±, Œ≤) :=
  ‚ü®fun f =>
    { toFun := f
      continuous_toFun := map_continuous f
      has_compact_support' := has_compact_support f }‚ü©

@[simp]
theorem coe_toContinuousMap (f : C_c(Œ±, Œ≤)) : (f.toContinuousMap : Œ± ‚Üí Œ≤) = f :=
  rfl

@[ext]
theorem ext {f g : C_c(Œ±, Œ≤)} (h : ‚àÄ x, f x = g x) : f = g :=
  DFunLike.ext _ _ h

/-- Copy of a `CompactlySupportedContinuousMap` with a new `toFun` equal to the old one. Useful
to fix definitional equalities. -/
protected def copy (f : C_c(Œ±, Œ≤)) (f' : Œ± ‚Üí Œ≤) (h : f' = f) : C_c(Œ±, Œ≤) where
  toFun := f'
  continuous_toFun := by
    rw [h]
    exact f.continuous_toFun
  has_compact_support' := by
    simp_rw [h]
    exact f.has_compact_support'

@[simp]
theorem coe_copy (f : C_c(Œ±, Œ≤)) (f' : Œ± ‚Üí Œ≤) (h : f' = f) : ‚áë(f.copy f' h) = f' :=
  rfl

theorem copy_eq (f : C_c(Œ±, Œ≤)) (f' : Œ± ‚Üí Œ≤) (h : f' = f) : f.copy f' h = f :=
  DFunLike.ext' h

theorem eq_of_empty [IsEmpty Œ±] (f g : C_c(Œ±, Œ≤)) : f = g :=
  ext <| IsEmpty.elim ‚Äπ_‚Ä∫

/-- A continuous function on a compact space has automatically compact support. -/
@[simps]
def ContinuousMap.liftCompactlySupported [CompactSpace Œ±] : C(Œ±, Œ≤) ‚âÉ C_c(Œ±, Œ≤) where
  toFun f :=
    { toFun := f
      continuous_toFun := f.continuous
      has_compact_support' := by
        simp only
        exact IsCompact.of_isClosed_subset isCompact_univ (isClosed_tsupport f)
          (Set.subset_univ (tsupport f))
        }
  invFun f := f
  left_inv f := by
    ext
    rfl
  right_inv f := by
    ext
    rfl

/-- A continuous function on a compact space has automatically compact support. This is not an
instance to avoid type class loops. -/
lemma compactlySupportedContinuousMapClass.ofCompact {G : Type*} [FunLike G Œ± Œ≤]
    [ContinuousMapClass G Œ± Œ≤] [CompactSpace Œ±] : CompactlySupportedContinuousMapClass G Œ± Œ≤ where
  map_continuous := map_continuous
  has_compact_support := by
    intro f
    exact IsCompact.of_isClosed_subset isCompact_univ (isClosed_tsupport f)
      (Set.subset_univ (tsupport f))

end Basics

/-! ### Algebraic structure

Whenever `Œ≤` has the structore of continuous addtive monoid and a compatible topological structure,
then `C_c(Œ±, Œ≤)` inherits a corresponding algebraic structure. The primary exception to this is that
`C_c(Œ±, Œ≤)` will not have a multiplicative identity.
-/

section AlgebraicStructure

section AddMonoid

variable [TopologicalSpace Œ≤] (x : Œ±)

variable [AddMonoid Œ≤] [ContinuousAdd Œ≤] (f g : C_c(Œ±, Œ≤))

instance instZero [Zero Œ≤] : Zero C_c(Œ±, Œ≤) where
  zero := { toFun := (0 : C(Œ±, Œ≤))
            continuous_toFun := (0 : C(Œ±, Œ≤)).2
            has_compact_support' := by
              rw [HasCompactSupport, tsupport]
              simp only [ContinuousMap.coe_zero, Function.support_zero', closure_empty,
                isCompact_empty]
}

instance instInhabited [Zero Œ≤] : Inhabited C_c(Œ±, Œ≤) :=
  ‚ü®0‚ü©

@[simp]
theorem coe_zero [Zero Œ≤] : ‚áë(0 : C_c(Œ±, Œ≤)) = 0 :=
  rfl

theorem zero_apply [Zero Œ≤] : (0 : C_c(Œ±, Œ≤)) x = 0 :=
  rfl

instance instMul [MulZeroClass Œ≤] [ContinuousMul Œ≤] : Mul C_c(Œ±, Œ≤) :=
  ‚ü®fun f g => ‚ü®f * g, HasCompactSupport.mul_left g.2‚ü©‚ü©

@[simp]
theorem coe_mul [MulZeroClass Œ≤] [ContinuousMul Œ≤] (f g : C_c(Œ±, Œ≤)) : ‚áë(f * g) = f * g :=
  rfl

theorem mul_apply [MulZeroClass Œ≤] [ContinuousMul Œ≤] (f g : C_c(Œ±, Œ≤)) : (f * g) x = f x * g x :=
  rfl

instance instMulZeroClass [MulZeroClass Œ≤] [ContinuousMul Œ≤] : MulZeroClass C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.mulZeroClass _ coe_zero coe_mul

instance instSemigroupWithZero [SemigroupWithZero Œ≤] [ContinuousMul Œ≤] :
    SemigroupWithZero C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.semigroupWithZero _ coe_zero coe_mul

instance instAdd [AddMonoid Œ≤] [ContinuousAdd Œ≤] : Add C_c(Œ±, Œ≤) :=
  ‚ü®fun f g => ‚ü®f + g, HasCompactSupport.add f.2 g.2‚ü©‚ü©

@[simp]
theorem coe_add [AddMonoid Œ≤] [ContinuousAdd Œ≤] (f g : C_c(Œ±, Œ≤)) : ‚áë(f + g) = f + g :=
  rfl

theorem add_apply [AddMonoid Œ≤] [ContinuousAdd Œ≤] (f g : C_c(Œ±, Œ≤)) : (f + g) x = f x + g x :=
  rfl

instance instAddZeroClass [AddMonoid Œ≤] [ContinuousAdd Œ≤] : AddZeroClass C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.addZeroClass _ coe_zero coe_add

instance instSMul {R : Type*} [MonoidWithZero R] [SMulWithZero R Œ≤] [ContinuousConstSMul R Œ≤] :
    SMul R C_c(Œ±, Œ≤) :=
  -- Porting note: Original version didn't have `Continuous.const_smul f.continuous r`
  ‚ü®fun r f => ‚ü®‚ü®r ‚Ä¢ ‚áëf, Continuous.const_smul f.continuous r‚ü©, HasCompactSupport.smul_left' f.2‚ü©‚ü©

@[simp, norm_cast]
theorem coe_smul {R : Type*} [MonoidWithZero R] [SMulWithZero R Œ≤] [ContinuousConstSMul R Œ≤] (r : R)
    (f : C_c(Œ±, Œ≤)) : ‚áë(r ‚Ä¢ f) = r ‚Ä¢ ‚áëf :=
  rfl

theorem smul_apply {R : Type*} [MonoidWithZero R] [SMulWithZero R Œ≤] [ContinuousConstSMul R Œ≤]
    (r : R) (f : C_c(Œ±, Œ≤)) (x : Œ±) : (r ‚Ä¢ f) x = r ‚Ä¢ f x :=
  rfl

instance instAddMonoid : AddMonoid C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.addMonoid _ coe_zero coe_add fun _ _ => rfl

end AddMonoid

variable [TopologicalSpace Œ≤] (x : Œ±)

variable [AddCommMonoid Œ≤] [ContinuousAdd Œ≤] (f g : C_c(Œ±, Œ≤))

instance instAddCommMonoid [AddCommMonoid Œ≤] [ContinuousAdd Œ≤] : AddCommMonoid C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.addCommMonoid _ coe_zero coe_add fun _ _ => rfl

section AddGroup

variable [AddGroup Œ≤] [TopologicalAddGroup Œ≤] (f g : C_c(Œ±, Œ≤))

instance instNeg : Neg C_c(Œ±, Œ≤) where
  neg f := {  toFun := -f.1
              continuous_toFun := map_continuous (-f.1)
              has_compact_support' := by
                rw [HasCompactSupport, tsupport]
                simp only [ContinuousMap.coe_neg, Function.support_neg']
                exact f.2 }

@[simp]
theorem coe_neg : ‚áë(-f) = -f :=
  rfl

theorem neg_apply : (-f) x = -f x :=
  rfl

instance instSub : Sub C_c(Œ±, Œ≤) where
  sub f g := {  toFun := f.1 - g.1
                continuous_toFun := map_continuous (f.1 - g.1)
                has_compact_support' := by
                  rw [HasCompactSupport, tsupport]
                  simp only [coe_toContinuousMap]
                  rw [sub_eq_add_neg]
                  apply HasCompactSupport.add f.2
                  rw [HasCompactSupport, tsupport]
                  simp only [ContinuousMap.coe_neg, Function.support_neg']
                  exact g.2 }

@[simp]
theorem coe_sub : ‚áë(f - g) = f - g :=
  rfl

theorem sub_apply : (f - g) x = f x - g x :=
  rfl

instance instAddGroup : AddGroup C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.addGroup _ coe_zero coe_add coe_neg coe_sub (fun _ _ => rfl) fun _ _ => rfl

end AddGroup

instance instAddCommGroup [AddCommGroup Œ≤] [TopologicalAddGroup Œ≤] : AddCommGroup C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.addCommGroup _ coe_zero coe_add coe_neg coe_sub (fun _ _ => rfl) fun _ _ =>
    rfl

instance instIsCentralScalar [AddCommMonoid Œ≤] {R : Type*} [MonoidWithZero R] [SMulWithZero R Œ≤]
    [SMulWithZero R·µê·µí·µñ Œ≤]
    [ContinuousConstSMul R Œ≤] [IsCentralScalar R Œ≤] : IsCentralScalar R C_c(Œ±, Œ≤) :=
  ‚ü®fun _ _ => ext fun _ => op_smul_eq_smul _ _‚ü©

instance instSMulWithZero [AddCommMonoid Œ≤] {R : Type*} [MonoidWithZero R] [SMulWithZero R Œ≤]
    [ContinuousConstSMul R Œ≤] : SMulWithZero R C_c(Œ±, Œ≤) :=
  Function.Injective.smulWithZero ‚ü®_, coe_zero‚ü© DFunLike.coe_injective coe_smul

instance instMulActionWithZero [AddCommMonoid Œ≤] {R : Type*} [MonoidWithZero R]
    [MulActionWithZero R Œ≤]
    [ContinuousConstSMul R Œ≤] : MulActionWithZero R C_c(Œ±, Œ≤) :=
  Function.Injective.mulActionWithZero ‚ü®_, coe_zero‚ü© DFunLike.coe_injective coe_smul

instance instModule [AddCommMonoid Œ≤] [ContinuousAdd Œ≤] {R : Type*} [Semiring R] [Module R Œ≤]
    [ContinuousConstSMul R Œ≤] : Module R C_c(Œ±, Œ≤) :=
  Function.Injective.module R ‚ü®‚ü®_, coe_zero‚ü©, coe_add‚ü© DFunLike.coe_injective coe_smul

instance instNonUnitalNonAssocSemiring [NonUnitalNonAssocSemiring Œ≤] [TopologicalSemiring Œ≤] :
    NonUnitalNonAssocSemiring C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.nonUnitalNonAssocSemiring _ coe_zero coe_add coe_mul fun _ _ => rfl

instance instNonUnitalSemiring [NonUnitalSemiring Œ≤] [TopologicalSemiring Œ≤] :
    NonUnitalSemiring C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.nonUnitalSemiring _ coe_zero coe_add coe_mul fun _ _ => rfl

instance instNonUnitalCommSemiring [NonUnitalCommSemiring Œ≤] [TopologicalSemiring Œ≤] :
    NonUnitalCommSemiring C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.nonUnitalCommSemiring _ coe_zero coe_add coe_mul fun _ _ => rfl

instance instNonUnitalNonAssocRing [NonUnitalNonAssocRing Œ≤] [TopologicalRing Œ≤] :
    NonUnitalNonAssocRing C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.nonUnitalNonAssocRing _ coe_zero coe_add coe_mul coe_neg coe_sub
    (fun _ _ => rfl) fun _ _ => rfl

instance instNonUnitalRing [NonUnitalRing Œ≤] [TopologicalRing Œ≤] : NonUnitalRing C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.nonUnitalRing _ coe_zero coe_add coe_mul coe_neg coe_sub (fun _ _ => rfl)
    fun _ _ => rfl

instance instNonUnitalCommRing [NonUnitalCommRing Œ≤] [TopologicalRing Œ≤] :
    NonUnitalCommRing C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.nonUnitalCommRing _ coe_zero coe_add coe_mul coe_neg coe_sub
    (fun _ _ => rfl) fun _ _ => rfl

instance instIsScalarTower {R : Type*} [Semiring R] [NonUnitalNonAssocRing Œ≤]
    [TopologicalSemiring Œ≤] [Module R Œ≤] [MonoidWithZero R] [SMulWithZero R Œ≤]
    [ContinuousConstSMul R Œ≤] [IsScalarTower R Œ≤ Œ≤] :
    IsScalarTower R C_c(Œ±, Œ≤) C_c(Œ±, Œ≤) where
  smul_assoc r f g := by
    ext
    simp only [smul_eq_mul, coe_mul, coe_smul, Pi.mul_apply, Pi.smul_apply]
    rw [‚Üê smul_eq_mul, ‚Üê smul_eq_mul, smul_assoc]

instance instSMulCommClass {R : Type*} [Semiring R] [NonUnitalNonAssocRing Œ≤]
    [TopologicalSemiring Œ≤] [Module R Œ≤] [MonoidWithZero R] [SMulWithZero R Œ≤]
    [ContinuousConstSMul R Œ≤] [SMulCommClass R Œ≤ Œ≤] :
    SMulCommClass R C_c(Œ±, Œ≤) C_c(Œ±, Œ≤) where
  smul_comm r f g := by
    ext
    simp only [smul_eq_mul, coe_smul, coe_mul, Pi.smul_apply, Pi.mul_apply]
    rw [‚Üê smul_eq_mul, ‚Üê smul_eq_mul, smul_comm]

end AlgebraicStructure

section Uniform

variable [UniformSpace Œ≤] [UniformSpace Œ≥] [Zero Œ≥]
variable [FunLike F Œ≤ Œ≥] [CompactlySupportedContinuousMapClass F Œ≤ Œ≥]

lemma zero_at_infty_of_hasCompactSupport [TopologicalSpace Œ≤] [Zero Œ≤]
    (f : C_c(Œ±, Œ≤)) :
    Filter.Tendsto f (Filter.cocompact Œ±) (nhds 0) := by
  rw [_root_.tendsto_nhds]
  intro s _ hzero
  rw [Filter.mem_cocompact]
  use tsupport f
  constructor
  ¬∑ exact f.2
  ¬∑ intro x hx
    simp only [Set.mem_preimage]
    rw [‚Üê Set.not_mem_compl_iff, compl_compl] at hx
    rw [image_eq_zero_of_nmem_tsupport hx]
    exact hzero

theorem uniformContinuous (f : F) : UniformContinuous (f : Œ≤ ‚Üí Œ≥) :=
  (map_continuous f).uniformContinuous_of_tendsto_cocompact
    (zero_at_infty_of_hasCompactSupport ‚ü®‚ü®(f : Œ≤ ‚Üí Œ≥) , map_continuous f‚ü©, has_compact_support f‚ü©)

end Uniform

/-! ### Metric structure

When `Œ≤` is a metric space, then every element of `C_c(Œ±, Œ≤)` is bounded, and so there is a natural
inclusion map `CompactlySupportedContinuousMap.toBCF : C_c(Œ±, Œ≤) ‚Üí (Œ± ‚Üí·µá Œ≤)`. Via this map `C_c(Œ±, Œ≤)`
inherits a metric as the pullback of the metric on `Œ± ‚Üí·µá Œ≤`. Moreover, this map has closed range
in `Œ± ‚Üí·µá Œ≤` and consequently `C_c(Œ±, Œ≤)` is a complete space whenever `Œ≤` is complete.
-/


section Metric

open Metric Set

variable [PseudoMetricSpace Œ≤] [Zero Œ≤] [FunLike F Œ± Œ≤] [CompactlySupportedContinuousMapClass F Œ± Œ≤]

protected theorem bounded (f : F) : ‚àÉ C, ‚àÄ x y : Œ±, dist ((f : Œ± ‚Üí Œ≤) x) (f y) ‚â§ C := by
  obtain ‚ü®C, hC‚ü© := Metric.isBounded_iff_nndist.mp
    ((has_compact_support f).isCompact_range (map_continuous f)).isBounded
  use C
  intro x y
  exact hC (Set.mem_range_self x) (Set.mem_range_self y)


theorem isBounded_range (f : C_c(Œ±, Œ≤)) : IsBounded (range f) :=
  isBounded_range_iff.2 (CompactlySupportedContinuousMap.bounded f)

theorem isBounded_image (f : C_c(Œ±, Œ≤)) (s : Set Œ±) : IsBounded (f '' s) :=
  f.isBounded_range.subset <| image_subset_range _ _

instance (priority := 100) instBoundedContinuousMapClass : BoundedContinuousMapClass F Œ± Œ≤ :=
  { ‚ÄπCompactlySupportedContinuousMapClass F Œ± Œ≤‚Ä∫ with
    map_bounded := fun f => CompactlySupportedContinuousMap.bounded f }

/-- Construct a bounded continuous function from a continuous function vanishing at infinity. -/
@[simps!]
def toBCF (f : C_c(Œ±, Œ≤)) : Œ± ‚Üí·µá Œ≤ :=
  ‚ü®f, map_bounded f‚ü©

section

variable (Œ±) (Œ≤)

theorem toBCF_injective : Function.Injective (toBCF : C_c(Œ±, Œ≤) ‚Üí Œ± ‚Üí·µá Œ≤) := fun f g h => by
  ext x
  simpa only using DFunLike.congr_fun h x

end

variable {C : ‚Ñù} {f g : C_c(Œ±, Œ≤)}

/-- The type of continuous functions vanishing at infinity, with the uniform distance induced by the
inclusion `CompactlySupportedContinuousMap.toBCF`, is a pseudo-metric space. -/
noncomputable instance instPseudoMetricSpace : PseudoMetricSpace C_c(Œ±, Œ≤) :=
  PseudoMetricSpace.induced toBCF inferInstance

/-- The type of continuous functions vanishing at infinity, with the uniform distance induced by the
inclusion `CompactlySupportedContinuousMap.toBCF`, is a metric space. -/
noncomputable instance instMetricSpace {Œ≤ : Type*} [MetricSpace Œ≤] [Zero Œ≤] :
    MetricSpace C_c(Œ±, Œ≤) :=
  MetricSpace.induced _ (toBCF_injective Œ± Œ≤) inferInstance

@[simp]
theorem dist_toBCF_eq_dist {f g : C_c(Œ±, Œ≤)} : dist f.toBCF g.toBCF = dist f g :=
  rfl

open BoundedContinuousFunction

/-- Convergence in the metric on `C_c(Œ±, Œ≤)` is uniform convergence. -/
theorem tendsto_iff_tendstoUniformly {Œπ : Type*} {F : Œπ ‚Üí C_c(Œ±, Œ≤)} {f : C_c(Œ±, Œ≤)} {l : Filter Œπ} :
    Tendsto F l (ùìù f) ‚Üî TendstoUniformly (fun i => F i) f l := by
  simpa only [Metric.tendsto_nhds] using
    @BoundedContinuousFunction.tendsto_iff_tendstoUniformly _ _ _ _ _ (fun i => (F i).toBCF)
      f.toBCF l

theorem isometry_toBCF : Isometry (toBCF : C_c(Œ±, Œ≤) ‚Üí Œ± ‚Üí·µá Œ≤) := by tauto

end Metric

section Norm

/-! ### Normed space

The norm structure on `C_c(Œ±, Œ≤)` is the one induced by the inclusion `toBCF : C_c(Œ±, Œ≤) ‚Üí (Œ± ‚Üí·µá b)`,
viewed as an additive monoid homomorphism. Then `C_c(Œ±, Œ≤)` is naturally a normed space over a normed
field `ùïú` whenever `Œ≤` is as well.
-/


section NormedSpace

noncomputable instance instSeminormedAddCommGroup [SeminormedAddCommGroup Œ≤] :
    SeminormedAddCommGroup C_c(Œ±, Œ≤) :=
  SeminormedAddCommGroup.induced _ _ (‚ü®‚ü®toBCF, rfl‚ü©, fun _ _ => rfl‚ü© : C_c(Œ±, Œ≤) ‚Üí+ Œ± ‚Üí·µá Œ≤)

noncomputable instance instNormedAddCommGroup [NormedAddCommGroup Œ≤] :
    NormedAddCommGroup C_c(Œ±, Œ≤) :=
  NormedAddCommGroup.induced _ _ (‚ü®‚ü®toBCF, rfl‚ü©, fun _ _ => rfl‚ü© : C_c(Œ±, Œ≤) ‚Üí+ Œ± ‚Üí·µá Œ≤)
    (toBCF_injective Œ± Œ≤)

variable [SeminormedAddCommGroup Œ≤] {ùïú : Type*} [NormedField ùïú] [NormedSpace ùïú Œ≤]

@[simp]
theorem norm_toBCF_eq_norm {f : C_c(Œ±, Œ≤)} : ‚Äñf.toBCF‚Äñ = ‚Äñf‚Äñ :=
  rfl

instance : NormedSpace ùïú C_c(Œ±, Œ≤) where norm_smul_le k f := (norm_smul_le k f.toBCF : _)

end NormedSpace

section NormedRing

noncomputable instance instNonUnitalSeminormedRing [NonUnitalSeminormedRing Œ≤] :
    NonUnitalSeminormedRing C_c(Œ±, Œ≤) :=
  { instNonUnitalRing, instSeminormedAddCommGroup with
    norm_mul := fun f g => norm_mul_le f.toBCF g.toBCF }

noncomputable instance instNonUnitalNormedRing [NonUnitalNormedRing Œ≤] :
    NonUnitalNormedRing C_c(Œ±, Œ≤) :=
  { instNonUnitalRing, instNormedAddCommGroup with
    norm_mul := fun f g => norm_mul_le f.toBCF g.toBCF }

noncomputable instance instNonUnitalSeminormedCommRing [NonUnitalSeminormedCommRing Œ≤] :
    NonUnitalSeminormedCommRing C_c(Œ±, Œ≤) :=
  { instNonUnitalSeminormedRing, instNonUnitalCommRing with }

noncomputable instance instNonUnitalNormedCommRing [NonUnitalNormedCommRing Œ≤] :
    NonUnitalNormedCommRing C_c(Œ±, Œ≤) :=
  { instNonUnitalNormedRing, instNonUnitalCommRing with }

end NormedRing

end Norm

section Star

/-! ### Star structure

It is possible to equip `C_c(Œ±, Œ≤)` with a pointwise `star` operation whenever there is a continuous
`star : Œ≤ ‚Üí Œ≤` for which `star (0 : Œ≤) = 0`. We don't have quite this weak a typeclass, but
`StarAddMonoid` is close enough.

The `StarAddMonoid` and `NormedStarGroup` classes on `C_c(Œ±, Œ≤)` are inherited from their
counterparts on `Œ± ‚Üí·µá Œ≤`. Ultimately, when `Œ≤` is a C‚ãÜ-ring, then so is `C_c(Œ±, Œ≤)`.
-/


variable [TopologicalSpace Œ≤] [AddMonoid Œ≤] [StarAddMonoid Œ≤] [ContinuousStar Œ≤]

theorem Function.support_star (f : Œ± ‚Üí Œ≤) :
    (Function.support fun (x : Œ±) => star (f x)) = Function.support f := by
  ext x
  simp only [Function.mem_support, ne_eq, star_eq_zero]

instance instStar : Star C_c(Œ±, Œ≤) where
  star f :=
    { toFun := fun x => star (f x)
      continuous_toFun := (map_continuous f).star
      has_compact_support' := by
        rw [HasCompactSupport, tsupport]
        simp only
        rw [Function.support_star f]
        exact f.2
    }

@[simp]
theorem coe_star (f : C_c(Œ±, Œ≤)) : ‚áë(star f) = star (‚áëf) :=
  rfl

theorem star_apply (f : C_c(Œ±, Œ≤)) (x : Œ±) : (star f) x = star (f x) :=
  rfl

instance instStarAddMonoid [ContinuousAdd Œ≤] : StarAddMonoid C_c(Œ±, Œ≤) where
  star_involutive f := ext fun x => star_star (f x)
  star_add f g := ext fun x => star_add (f x) (g x)

end Star

section NormedStar

variable [NormedAddCommGroup Œ≤] [StarAddMonoid Œ≤] [NormedStarGroup Œ≤]

instance instNormedStarGroup : NormedStarGroup C_c(Œ±, Œ≤) where
  norm_star f := (norm_star f.toBCF : _)

end NormedStar

section StarModule

variable {ùïú : Type*} [Zero ùïú] [Star ùïú] [AddMonoid Œ≤] [StarAddMonoid Œ≤] [TopologicalSpace Œ≤]
  [ContinuousAdd Œ≤] [ContinuousStar Œ≤] [MonoidWithZero ùïú] [SMulWithZero ùïú Œ≤]
  [ContinuousConstSMul ùïú Œ≤] [StarModule ùïú Œ≤]

instance instStarModule : StarModule ùïú C_c(Œ±, Œ≤) where
  star_smul k f := ext fun x => star_smul k (f x)

end StarModule

section StarRing

variable [NonUnitalSemiring Œ≤] [StarRing Œ≤] [TopologicalSpace Œ≤] [ContinuousStar Œ≤]
  [TopologicalSemiring Œ≤]

instance instStarRing : StarRing C_c(Œ±, Œ≤) :=
  { CompactlySupportedContinuousMap.instStarAddMonoid with
    star_mul := fun f g => ext fun x => star_mul (f x) (g x) }

end StarRing

section CstarRing

instance instCstarRing [NonUnitalNormedRing Œ≤] [StarRing Œ≤] [CstarRing Œ≤] : CstarRing C_c(Œ±, Œ≤) where
  norm_star_mul_self {f} := CstarRing.norm_star_mul_self (x := f.toBCF)

end CstarRing

/-! ### C_c as a functor

For each `Œ≤` with sufficient structure, there is a contravariant functor `C_c(-, Œ≤)` from the
category of topological spaces with morphisms given by `CocompactMap`s.
-/


variable {Œ¥ : Type*} [TopologicalSpace Œ≤] [TopologicalSpace Œ≥] [TopologicalSpace Œ¥]

local notation Œ± " ‚Üíco " Œ≤ => CocompactMap Œ± Œ≤

section

variable [T2Space Œ≥] [Zero Œ¥]

/-- Composition of a continuous function with compact support on a `T2Space` with a cocompact map
yields another continuous function with compact support. -/
def comp (f : C_c(Œ≥, Œ¥)) (g : Œ≤ ‚Üíco Œ≥) : C_c(Œ≤, Œ¥) where
  toContinuousMap := (f : C(Œ≥, Œ¥)).comp g
  has_compact_support' := by
    simp only [ContinuousMap.toFun_eq_coe, ContinuousMap.coe_comp, ContinuousMap.coe_coe]
    rw [HasCompactSupport]
    apply IsCompact.of_isClosed_subset
    exact CocompactMap.isCompact_preimage g (hasCompactSupport_def.mp f.2)
    exact isClosed_tsupport (f ‚àò g)
    simp only [ContinuousMap.toFun_eq_coe, coe_toContinuousMap]
    intro x hx
    simp only [Set.mem_preimage]
    rw [_root_.mem_closure_iff]
    intro o ho hgxo
    rw [tsupport, _root_.mem_closure_iff] at hx
    obtain ‚ü®y, hy‚ü© := hx (g ‚Åª¬π' o) (IsOpen.preimage g.1.2 ho) hgxo
    use g y
    simp only [Set.mem_inter_iff, Set.mem_preimage, Function.mem_support, Function.comp_apply,
      ne_eq] at hy
    simp only [Set.mem_inter_iff, Function.mem_support, ne_eq]
    exact hy

@[simp]
theorem coe_comp_to_continuous_fun (f : C_c(Œ≥, Œ¥)) (g : Œ≤ ‚Üíco Œ≥) : ((f.comp g) : Œ≤ ‚Üí Œ¥) = f ‚àò g :=
  rfl

@[simp]
theorem comp_id (f : C_c(Œ≥, Œ¥)) : f.comp (CocompactMap.id Œ≥) = f :=
  ext fun _ => rfl

variable [T2Space Œ≤]

@[simp]
theorem comp_assoc (f : C_c(Œ≥, Œ¥)) (g : Œ≤ ‚Üíco Œ≥) (h : Œ± ‚Üíco Œ≤) :
    (f.comp g).comp h = f.comp (g.comp h) :=
  rfl

@[simp]
theorem zero_comp (g : Œ≤ ‚Üíco Œ≥) : (0 : C_c(Œ≥, Œ¥)).comp g = 0 :=
  rfl

end

variable [T2Space Œ≥]

/-- Composition as an additive monoid homomorphism. -/
def compAddMonoidHom [AddMonoid Œ¥] [ContinuousAdd Œ¥] (g : Œ≤ ‚Üíco Œ≥) : C_c(Œ≥, Œ¥) ‚Üí+ C_c(Œ≤, Œ¥) where
  toFun f := f.comp g
  map_zero' := zero_comp g
  map_add' _ _ := rfl

/-- Composition as a semigroup homomorphism. -/
def compMulHom [MulZeroClass Œ¥] [ContinuousMul Œ¥] (g : Œ≤ ‚Üíco Œ≥) : C_c(Œ≥, Œ¥) ‚Üí‚Çô* C_c(Œ≤, Œ¥) where
  toFun f := f.comp g
  map_mul' _ _ := rfl

/-- Composition as a linear map. -/
def compLinearMap [AddCommMonoid Œ¥] [ContinuousAdd Œ¥] {R : Type*} [Semiring R] [Module R Œ¥]
    [ContinuousConstSMul R Œ¥] (g : Œ≤ ‚Üíco Œ≥) : C_c(Œ≥, Œ¥) ‚Üí‚Çó[R] C_c(Œ≤, Œ¥) where
  toFun f := f.comp g
  map_add' _ _ := rfl
  map_smul' _ _ := rfl

/-- Composition as a non-unital algebra homomorphism. -/
def compNonUnitalAlgHom {R : Type*} [Semiring R] [NonUnitalNonAssocSemiring Œ¥]
    [TopologicalSemiring Œ¥] [Module R Œ¥] [ContinuousConstSMul R Œ¥] (g : Œ≤ ‚Üíco Œ≥) :
    C_c(Œ≥, Œ¥) ‚Üí‚Çô‚Çê[R] C_c(Œ≤, Œ¥) where
  toFun f := f.comp g
  map_smul' _ _ := rfl
  map_zero' := rfl
  map_add' _ _ := rfl
  map_mul' _ _ := rfl

end CompactlySupportedContinuousMap
