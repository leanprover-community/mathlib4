/-
Copyright (c) 2022 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Topology.ContinuousFunction.Basic

#align_import topology.continuous_function.cocompact_map from "leanprover-community/mathlib"@"0a0ec35061ed9960bf0e7ffb0335f44447b58977"

/-!
# Cocompact continuous maps

The type of *cocompact continuous maps* are those which tend to the cocompact filter on the
codomain along the cocompact filter on the domain. When the domain and codomain are Hausdorff, this
is equivalent to many other conditions, including that preimages of compact sets are compact. -/


universe u v w

open Filter Set

/-! ### Cocompact continuous maps -/


/-- A *cocompact continuous map* is a continuous function between topological spaces which
tends to the cocompact filter along the cocompact filter. Functions for which preimages of compact
sets are compact always satisfy this property, and the converse holds for cocompact continuous maps
when the codomain is Hausdorff (see `CocompactMap.tendsto_of_forall_preimage` and
`CocompactMap.isCompact_preimage`).

Cocompact maps thus generalise proper maps, with which they correspond when the codomain is
Hausdorff. -/
structure CocompactMap (Œ± : Type u) (Œ≤ : Type v) [TopologicalSpace Œ±] [TopologicalSpace Œ≤] extends
  ContinuousMap Œ± Œ≤ : Type max u v where
  /-- The cocompact filter on `Œ±` tends to the cocompact filter on `Œ≤` under the function -/
  cocompact_tendsto' : Tendsto toFun (cocompact Œ±) (cocompact Œ≤)
#align cocompact_map CocompactMap

section

/-- `CocompactMapClass F Œ± Œ≤` states that `F` is a type of cocompact continuous maps.

You should also extend this typeclass when you extend `CocompactMap`. -/
class CocompactMapClass (F : Type*) (Œ± Œ≤ : outParam <| Type*) [TopologicalSpace Œ±]
  [TopologicalSpace Œ≤] extends ContinuousMapClass F Œ± Œ≤ where
  /-- The cocompact filter on `Œ±` tends to the cocompact filter on `Œ≤` under the function -/
  cocompact_tendsto (f : F) : Tendsto f (cocompact Œ±) (cocompact Œ≤)
#align cocompact_map_class CocompactMapClass

end

namespace CocompactMapClass

variable {F Œ± Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [CocompactMapClass F Œ± Œ≤]

/-- Turn an element of a type `F` satisfying `CocompactMapClass F Œ± Œ≤` into an actual
`CocompactMap`. This is declared as the default coercion from `F` to `CocompactMap Œ± Œ≤`. -/
@[coe]
def toCocompactMap (f : F) : CocompactMap Œ± Œ≤ :=
  { (f : C(Œ±, Œ≤)) with
    cocompact_tendsto' := cocompact_tendsto f }

instance : CoeTC F (CocompactMap Œ± Œ≤) :=
  ‚ü®toCocompactMap‚ü©

end CocompactMapClass

export CocompactMapClass (cocompact_tendsto)

namespace CocompactMap

section Basics

variable {Œ± Œ≤ Œ≥ Œ¥ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [TopologicalSpace Œ≥]
  [TopologicalSpace Œ¥]

instance : CocompactMapClass (CocompactMap Œ± Œ≤) Œ± Œ≤ where
  coe f := f.toFun
  coe_injective' f g h := by
    obtain ‚ü®‚ü®_, _‚ü©, _‚ü© := f
    -- ‚ä¢ { toContinuousMap := ContinuousMap.mk toFun‚úù, cocompact_tendsto' := cocompac ‚Ä¶
    obtain ‚ü®‚ü®_, _‚ü©, _‚ü© := g
    -- ‚ä¢ { toContinuousMap := ContinuousMap.mk toFun‚úù¬π, cocompact_tendsto' := cocompa ‚Ä¶
    congr
    -- üéâ no goals
  map_continuous f := f.continuous_toFun
  cocompact_tendsto f := f.cocompact_tendsto'

/- Porting note: not needed anymore
/-- Helper instance for when there's too many metavariables to apply `fun_like.has_coe_to_fun`
directly. -/
instance : CoeFun (CocompactMap Œ± Œ≤) fun _ => Œ± ‚Üí Œ≤ :=
  FunLike.hasCoeToFun-/

@[simp]
theorem coe_toContinuousMap {f : CocompactMap Œ± Œ≤} : (f.toContinuousMap : Œ± ‚Üí Œ≤) = f :=
  rfl
#align cocompact_map.coe_to_continuous_fun CocompactMap.coe_toContinuousMap

@[ext]
theorem ext {f g : CocompactMap Œ± Œ≤} (h : ‚àÄ x, f x = g x) : f = g :=
  FunLike.ext _ _ h
#align cocompact_map.ext CocompactMap.ext

/-- Copy of a `CocompactMap` with a new `toFun` equal to the old one. Useful
to fix definitional equalities. -/
protected def copy (f : CocompactMap Œ± Œ≤) (f' : Œ± ‚Üí Œ≤) (h : f' = f) : CocompactMap Œ± Œ≤ where
  toFun := f'
  continuous_toFun := by
    rw [h]
    -- ‚ä¢ Continuous ‚Üëf
    exact f.continuous_toFun
    -- üéâ no goals
  cocompact_tendsto' := by
    simp_rw [h]
    -- ‚ä¢ Tendsto (‚Üëf) (cocompact Œ±) (cocompact Œ≤)
    exact f.cocompact_tendsto'
    -- üéâ no goals
#align cocompact_map.copy CocompactMap.copy

@[simp]
theorem coe_copy (f : CocompactMap Œ± Œ≤) (f' : Œ± ‚Üí Œ≤) (h : f' = f) : ‚áë(f.copy f' h) = f' :=
  rfl
#align cocompact_map.coe_copy CocompactMap.coe_copy

theorem copy_eq (f : CocompactMap Œ± Œ≤) (f' : Œ± ‚Üí Œ≤) (h : f' = f) : f.copy f' h = f :=
  FunLike.ext' h
#align cocompact_map.copy_eq CocompactMap.copy_eq

@[simp]
theorem coe_mk (f : C(Œ±, Œ≤)) (h : Tendsto f (cocompact Œ±) (cocompact Œ≤)) :
    ‚áë(‚ü®f, h‚ü© : CocompactMap Œ± Œ≤) = f :=
  rfl
#align cocompact_map.coe_mk CocompactMap.coe_mk

section

variable (Œ±)

/-- The identity as a cocompact continuous map. -/
protected def id : CocompactMap Œ± Œ± :=
  ‚ü®ContinuousMap.id _, tendsto_id‚ü©
#align cocompact_map.id CocompactMap.id

@[simp]
theorem coe_id : ‚áë(CocompactMap.id Œ±) = id :=
  rfl
#align cocompact_map.coe_id CocompactMap.coe_id

end

instance : Inhabited (CocompactMap Œ± Œ±) :=
  ‚ü®CocompactMap.id Œ±‚ü©

/-- The composition of cocompact continuous maps, as a cocompact continuous map. -/
def comp (f : CocompactMap Œ≤ Œ≥) (g : CocompactMap Œ± Œ≤) : CocompactMap Œ± Œ≥ :=
  ‚ü®f.toContinuousMap.comp g, (cocompact_tendsto f).comp (cocompact_tendsto g)‚ü©
#align cocompact_map.comp CocompactMap.comp

@[simp]
theorem coe_comp (f : CocompactMap Œ≤ Œ≥) (g : CocompactMap Œ± Œ≤) : ‚áë(comp f g) = f ‚àò g :=
  rfl
#align cocompact_map.coe_comp CocompactMap.coe_comp

@[simp]
theorem comp_apply (f : CocompactMap Œ≤ Œ≥) (g : CocompactMap Œ± Œ≤) (a : Œ±) : comp f g a = f (g a) :=
  rfl
#align cocompact_map.comp_apply CocompactMap.comp_apply

@[simp]
theorem comp_assoc (f : CocompactMap Œ≥ Œ¥) (g : CocompactMap Œ≤ Œ≥) (h : CocompactMap Œ± Œ≤) :
    (f.comp g).comp h = f.comp (g.comp h) :=
  rfl
#align cocompact_map.comp_assoc CocompactMap.comp_assoc

@[simp]
theorem id_comp (f : CocompactMap Œ± Œ≤) : (CocompactMap.id _).comp f = f :=
  ext fun _ => rfl
#align cocompact_map.id_comp CocompactMap.id_comp

@[simp]
theorem comp_id (f : CocompactMap Œ± Œ≤) : f.comp (CocompactMap.id _) = f :=
  ext fun _ => rfl
#align cocompact_map.comp_id CocompactMap.comp_id

theorem tendsto_of_forall_preimage {f : Œ± ‚Üí Œ≤} (h : ‚àÄ s, IsCompact s ‚Üí IsCompact (f ‚Åª¬π' s)) :
    Tendsto f (cocompact Œ±) (cocompact Œ≤) := fun s hs =>
  match mem_cocompact.mp hs with
  | ‚ü®t, ht, hts‚ü© =>
    mem_map.mpr (mem_cocompact.mpr ‚ü®f ‚Åª¬π' t, h t ht, by simpa using preimage_mono hts‚ü©)
                                                        -- üéâ no goals
#align cocompact_map.tendsto_of_forall_preimage CocompactMap.tendsto_of_forall_preimage

/-- If the codomain is Hausdorff, preimages of compact sets are compact under a cocompact
continuous map. -/
theorem isCompact_preimage [T2Space Œ≤] (f : CocompactMap Œ± Œ≤) ‚¶És : Set Œ≤‚¶Ñ (hs : IsCompact s) :
    IsCompact (f ‚Åª¬π' s) := by
  obtain ‚ü®t, ht, hts‚ü© :=
    mem_cocompact'.mp
      (by
        simpa only [preimage_image_preimage, preimage_compl] using
          mem_map.mp
            (cocompact_tendsto f <|
              mem_cocompact.mpr ‚ü®s, hs, compl_subset_compl.mpr (image_preimage_subset f _)‚ü©))
  exact
    isCompact_of_isClosed_subset ht (hs.isClosed.preimage <| map_continuous f) (by simpa using hts)
#align cocompact_map.is_compact_preimage CocompactMap.isCompact_preimage

end Basics

end CocompactMap

/-- A homeomorphism is a cocompact map. -/
@[simps]
def Homeomorph.toCocompactMap {Œ± Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤]
    (f : Œ± ‚âÉ‚Çú Œ≤) : CocompactMap Œ± Œ≤ where
  toFun := f
  continuous_toFun := f.continuous
  cocompact_tendsto' := by
    refine' CocompactMap.tendsto_of_forall_preimage fun K hK => _
    -- ‚ä¢ IsCompact ((ContinuousMap.mk ‚Üëf).toFun ‚Åª¬π' K)
    erw [K.preimage_equiv_eq_image_symm]
    -- ‚ä¢ IsCompact (‚Üëf.symm '' K)
    exact hK.image f.symm.continuous
    -- üéâ no goals
#align homeomorph.to_cocompact_map Homeomorph.toCocompactMap
