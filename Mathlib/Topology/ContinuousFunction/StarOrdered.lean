/-
Copyright (c) 2024 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Analysis.Complex.Basic
import Mathlib.Topology.ContinuousFunction.Algebra
import Mathlib.Topology.ContinuousFunction.ContinuousMapZero

/-! # Continuous functions as a star-ordered ring -/

namespace ContinuousMap

variable {Î± : Type*} [TopologicalSpace Î±]

lemma starOrderedRing_of_sqrt {R : Type*} [PartialOrder R] [NonUnitalRing R] [StarRing R]
    [StarOrderedRing R] [TopologicalSpace R] [ContinuousStar R] [TopologicalRing R]
    (sqrt : R â†’ R) (h_continuousOn : ContinuousOn sqrt {x : R | 0 â‰¤ x})
    (h_sqrt : âˆ€ x, 0 â‰¤ x â†’ star (sqrt x) * sqrt x = x) : StarOrderedRing C(Î±, R) :=
  StarOrderedRing.of_nonneg_iff' add_le_add_left fun f â†¦ by
    constructor
    Â· intro hf
      use (mk _ h_continuousOn.restrict).comp âŸ¨_, map_continuous f |>.codRestrict (by exact hf Â·)âŸ©
      ext x
      exact h_sqrt (f x) (hf x) |>.symm
    Â· rintro âŸ¨f, rflâŸ©
      rw [ContinuousMap.le_def]
      exact fun x â†¦ star_mul_self_nonneg (f x)

open scoped ComplexOrder in
open RCLike in
instance (priority := 100) instStarOrderedRingRCLike {ğ•œ : Type*} [RCLike ğ•œ] :
    StarOrderedRing C(Î±, ğ•œ) :=
  starOrderedRing_of_sqrt ((â†‘) âˆ˜ Real.sqrt âˆ˜ re) (by fun_prop) fun x hx â†¦ by
    simp only [Function.comp_apply,star_def]
    obtain hx' := nonneg_iff.mp hx |>.right
    rw [â† conj_eq_iff_im, conj_eq_iff_re] at hx'
    rw [conj_ofReal, â† ofReal_mul, Real.mul_self_sqrt, hx']
    rw [nonneg_iff]
    simpa using nonneg_iff.mp hx |>.left

instance instStarOrderedRingReal : StarOrderedRing C(Î±, â„) :=
  instStarOrderedRingRCLike (ğ•œ := â„)

open scoped ComplexOrder in
open Complex in
instance instStarOrderedRingComplex : StarOrderedRing C(Î±, â„‚) :=
  instStarOrderedRingRCLike (ğ•œ := â„‚)

open NNReal in
instance instStarOrderedRingNNReal : StarOrderedRing C(Î±, â„â‰¥0) :=
  StarOrderedRing.of_le_iff fun f g â†¦ by
    constructor
    Â· intro hfg
      use .comp âŸ¨sqrt, by fun_propâŸ© (g - f)
      ext1 x
      simpa using add_tsub_cancel_of_le (hfg x) |>.symm
    Â· rintro âŸ¨s, rflâŸ©
      exact fun _ â†¦ by simp

end ContinuousMap

namespace ContinuousMapZero

variable {Î± : Type*} [TopologicalSpace Î±] [Zero Î±]

lemma starOrderedRing_of_sqrt {R : Type*} [PartialOrder R] [CommRing R] [StarRing R]
    [StarOrderedRing R] [TopologicalSpace R] [ContinuousStar R] [TopologicalRing R]
    (sqrt : R â†’ R) (h_continuous : Continuous sqrt)
    (sqrt_map_zero : sqrt 0 = 0)
    (h_sqrt : âˆ€ x, 0 â‰¤ x â†’ star (sqrt x) * sqrt x = x) : StarOrderedRing C(Î±, R)â‚€ := by
  refine StarOrderedRing.of_nonneg_iff' ?_ ?_
  Â· intro x y hxy z
    rw [ContinuousMapZero.le_def]
    intro i
    simp [hxy i]
  Â· intro x
    refine âŸ¨fun hf => ?_, ?_âŸ©
    Â· let sqrtC : C(R, R)â‚€ :=
      { toFun := sqrt
        continuous_toFun := h_continuous
        map_zero' := sqrt_map_zero }
      refine âŸ¨sqrtC.comp x, ?_âŸ©
      ext i
      simp [sqrtC, h_sqrt (x i) (hf i)]
    Â· rintro âŸ¨f, rflâŸ©
      rw [ContinuousMapZero.le_def]
      intro i
      simp [star_mul_self_nonneg (f i)]

open scoped ComplexOrder in
open RCLike in
instance (priority := 100) instStarOrderedRingRCLike {ğ•œ : Type*} [RCLike ğ•œ] :
    StarOrderedRing C(Î±, ğ•œ)â‚€ :=
  starOrderedRing_of_sqrt ((â†‘) âˆ˜ Real.sqrt âˆ˜ re) (by fun_prop) (by simp) <| fun x hx => by
    simp only [Function.comp_apply,star_def]
    obtain hx' := nonneg_iff.mp hx |>.right
    rw [â† conj_eq_iff_im, conj_eq_iff_re] at hx'
    rw [conj_ofReal, â† ofReal_mul, Real.mul_self_sqrt, hx']
    rw [nonneg_iff]
    simpa using nonneg_iff.mp hx |>.left

instance instStarOrderedRingReal : StarOrderedRing C(Î±, â„)â‚€ :=
  instStarOrderedRingRCLike (ğ•œ := â„)

open scoped ComplexOrder in
open Complex in
instance instStarOrderedRingComplex : StarOrderedRing C(Î±, â„‚)â‚€ :=
  instStarOrderedRingRCLike (ğ•œ := â„‚)

open NNReal in
instance instStarOrderedRingNNReal : StarOrderedRing C(Î±, â„â‰¥0)â‚€ :=
  StarOrderedRing.of_le_iff fun f g â†¦ by
    refine âŸ¨fun hfg => ?_, ?_âŸ©
    Â· let sqrtC : C(â„â‰¥0, â„â‰¥0)â‚€ :=
      { toFun := sqrt
        continuous_toFun := by fun_prop
        map_zero' := by simp }
      let g_sub_f : C(Î±, â„â‰¥0)â‚€ :=
      { toFun := g - f
        continuous_toFun := Continuous.sub (by fun_prop) (by fun_prop)
        map_zero' := by simp }
      use .comp sqrtC g_sub_f
      ext1 x
      simpa [sqrtC, g_sub_f] using add_tsub_cancel_of_le (hfg x) |>.symm
    Â· rintro âŸ¨s, rflâŸ©
      exact fun _ â†¦ by simp

end ContinuousMapZero
