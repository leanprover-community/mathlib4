/-
Copyright (c) 2024 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Topology.ContinuousFunction.Algebra
import Mathlib.Topology.ContinuousFunction.Compact

/-!
# Continuous maps sending zero to zero

This is the type of continuous maps from `X` to `R` such that `(0 : X) ‚Ü¶ (0 : R)` for which we
provide the scoped notation `C(X, R)‚ÇÄ`.  We provide this as a dedicated type solely for the
non-unital continuous functional calculus, as using various terms of type `Ideal C(X, R)` were
overly burdensome on type class synthesis.

Of course, one could generalize to maps between pointed topological spaces, but that goes beyond
the purpose of this type.
-/

assert_not_exists StarOrderedRing

open Set Function

/-- The type of continuous maps which map zero to zero.

Note that one should never use the structure projection `ContinuousMapZero.toContinuousMap` and
instead favor the coercion `(‚Üë) : C(X, R)‚ÇÄ ‚Üí C(X, R)` available from the instance of
`ContinuousMapClass`. All the instances on `C(X, R)‚ÇÄ` from `C(X, R)` passes through this coercion,
not the structure projection. Of course, the two are definitionally equal, but not reducibly so. -/
structure ContinuousMapZero (X R : Type*) [Zero X] [Zero R] [TopologicalSpace X]
    [TopologicalSpace R] extends C(X, R) where
  map_zero' : toContinuousMap 0 = 0

namespace ContinuousMapZero

@[inherit_doc]
scoped notation "C(" X ", " R ")‚ÇÄ" => ContinuousMapZero X R

section Basic

variable {X Y R : Type*} [Zero X] [Zero Y] [Zero R]
variable [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace R]

instance instFunLike : FunLike C(X, R)‚ÇÄ X R where
  coe f := f.toFun
  coe_injective' _ _ h := congr(‚ü®‚ü®$(h), _‚ü©, _‚ü©)

instance instContinuousMapClass : ContinuousMapClass C(X, R)‚ÇÄ X R where
  map_continuous f := f.continuous

instance instZeroHomClass : ZeroHomClass C(X, R)‚ÇÄ X R where
  map_zero f := f.map_zero'

@[ext]
lemma ext {f g : C(X, R)‚ÇÄ} (h : ‚àÄ x, f x = g x) : f = g := DFunLike.ext f g h

@[simp]
lemma coe_mk {f : C(X, R)} {h0 : f 0 = 0} : ‚áë(mk f h0) = f := rfl

lemma toContinuousMap_injective : Injective ((‚Üë) : C(X, R)‚ÇÄ ‚Üí C(X, R)) :=
  fun _ _ h ‚Ü¶ congr(.mk $(h) _)

lemma range_toContinuousMap : range ((‚Üë) : C(X, R)‚ÇÄ ‚Üí C(X, R)) = {f : C(X, R) | f 0 = 0} :=
  Set.ext fun f ‚Ü¶ ‚ü®fun ‚ü®f', hf'‚ü© ‚Ü¶ hf' ‚ñ∏ map_zero f', fun hf ‚Ü¶ ‚ü®‚ü®f, hf‚ü©, rfl‚ü©‚ü©

/-- Composition of continuous maps which map zero to zero. -/
def comp (g : C(Y, R)‚ÇÄ) (f : C(X, Y)‚ÇÄ) : C(X, R)‚ÇÄ where
  toContinuousMap := (g : C(Y, R)).comp (f : C(X, Y))
  map_zero' := show g (f 0) = 0 from map_zero f ‚ñ∏ map_zero g

@[simp]
lemma comp_apply (g : C(Y, R)‚ÇÄ) (f : C(X, Y)‚ÇÄ) (x : X) : g.comp f x = g (f x) := rfl

instance instPartialOrder [PartialOrder R] : PartialOrder C(X, R)‚ÇÄ :=
  .lift _ DFunLike.coe_injective'

lemma le_def [PartialOrder R] (f g : C(X, R)‚ÇÄ) : f ‚â§ g ‚Üî ‚àÄ x, f x ‚â§ g x := Iff.rfl

protected instance instTopologicalSpace : TopologicalSpace C(X, R)‚ÇÄ :=
  TopologicalSpace.induced ((‚Üë) : C(X, R)‚ÇÄ ‚Üí C(X, R)) inferInstance

lemma embedding_toContinuousMap : Embedding ((‚Üë) : C(X, R)‚ÇÄ ‚Üí C(X, R)) where
  induced := rfl
  inj _ _ h := ext fun x ‚Ü¶ congr($(h) x)

instance [T0Space R] : T0Space C(X, R)‚ÇÄ := embedding_toContinuousMap.t0Space
instance [T1Space R] : T1Space C(X, R)‚ÇÄ := embedding_toContinuousMap.t1Space
instance [T2Space R] : T2Space C(X, R)‚ÇÄ := embedding_toContinuousMap.t2Space

lemma closedEmbedding_toContinuousMap [T1Space R] :
    ClosedEmbedding ((‚Üë) : C(X, R)‚ÇÄ ‚Üí C(X, R)) where
  toEmbedding := embedding_toContinuousMap
  isClosed_range := by
    rw [range_toContinuousMap]
    exact isClosed_singleton.preimage <| ContinuousMap.continuous_eval_const 0

@[fun_prop]
lemma continuous_comp_left {X Y Z : Type*} [TopologicalSpace X]
    [TopologicalSpace Y] [TopologicalSpace Z] [Zero X] [Zero Y] [Zero Z] (f : C(X, Y)‚ÇÄ) :
    Continuous fun g : C(Y, Z)‚ÇÄ ‚Ü¶ g.comp f := by
  rw [continuous_induced_rng]
  show Continuous fun g : C(Y, Z)‚ÇÄ ‚Ü¶ (g : C(Y, Z)).comp (f : C(X, Y))
  fun_prop

/-- The identity function as an element of `C(s, R)‚ÇÄ` when `0 ‚àà (s : Set R)`. -/
@[simps!]
protected def id {s : Set R} [Zero s] (h0 : ((0 : s) : R) = 0) : C(s, R)‚ÇÄ :=
  ‚ü®.restrict s (.id R), h0‚ü©

@[simp]
lemma toContinuousMap_id {s : Set R} [Zero s] (h0 : ((0 : s) : R) = 0) :
    (ContinuousMapZero.id h0 : C(s, R)) = .restrict s (.id R) :=
  rfl

end Basic

section Algebra

variable {X R : Type*} [Zero X] [TopologicalSpace X]
variable [TopologicalSpace R]

instance instZero [Zero R] : Zero C(X, R)‚ÇÄ where
  zero := ‚ü®0, rfl‚ü©

@[simp] lemma coe_zero [Zero R] : ‚áë(0 : C(X, R)‚ÇÄ) = 0 := rfl

instance instAdd [AddZeroClass R] [ContinuousAdd R] : Add C(X, R)‚ÇÄ where
  add f g := ‚ü®f + g, by simp‚ü©

@[simp] lemma coe_add [AddZeroClass R] [ContinuousAdd R] (f g : C(X, R)‚ÇÄ) : ‚áë(f + g) = f + g := rfl

instance instMul [MulZeroClass R] [ContinuousMul R] : Mul C(X, R)‚ÇÄ where
  mul f g := ‚ü®f * g, by simp‚ü©

@[simp] lemma coe_mul [MulZeroClass R] [ContinuousMul R] (f g : C(X, R)‚ÇÄ) : ‚áë(f * g) = f * g := rfl

instance instSMul {M : Type*} [Zero R] [SMulZeroClass M R] [ContinuousConstSMul M R] :
    SMul M C(X, R)‚ÇÄ where
  smul m f := ‚ü®m ‚Ä¢ f, by simp‚ü©

@[simp] lemma coe_smul {M : Type*} [Zero R] [SMulZeroClass M R] [ContinuousConstSMul M R]
    (m : M) (f : C(X, R)‚ÇÄ) : ‚áë(m ‚Ä¢ f) = m ‚Ä¢ f := rfl

section Semiring

variable [CommSemiring R] [TopologicalSemiring R]

instance instNonUnitalCommSemiring : NonUnitalCommSemiring C(X, R)‚ÇÄ :=
  toContinuousMap_injective.nonUnitalCommSemiring
    _ rfl (fun _ _ ‚Ü¶ rfl) (fun _ _ ‚Ü¶ rfl) (fun _ _ ‚Ü¶ rfl)

instance instModule {M : Type*} [Semiring M] [Module M R] [ContinuousConstSMul M R] :
    Module M C(X, R)‚ÇÄ :=
  toContinuousMap_injective.module M
    { toFun := _, map_add' := fun _ _ ‚Ü¶ rfl, map_zero' := rfl } (fun _ _ ‚Ü¶ rfl)

instance instSMulCommClass {M N : Type*} [SMulZeroClass M R] [ContinuousConstSMul M R]
    [SMulZeroClass N R] [ContinuousConstSMul N R] [SMulCommClass M N R] :
    SMulCommClass M N C(X, R)‚ÇÄ where
  smul_comm _ _ _ := ext fun _ ‚Ü¶ smul_comm ..

instance instSMulCommClass' {M : Type*} [SMulZeroClass M R] [SMulCommClass M R R]
    [ContinuousConstSMul M R] : SMulCommClass M C(X, R)‚ÇÄ C(X, R)‚ÇÄ where
  smul_comm m f g := ext fun x ‚Ü¶ smul_comm m (f x) (g x)

instance instIsScalarTower {M N : Type*} [SMulZeroClass M R] [ContinuousConstSMul M R]
    [SMulZeroClass N R] [ContinuousConstSMul N R] [SMul M N] [IsScalarTower M N R] :
    IsScalarTower M N C(X, R)‚ÇÄ where
  smul_assoc _ _ _ := ext fun _ ‚Ü¶ smul_assoc ..

instance instIsScalarTower' {M : Type*} [SMulZeroClass M R] [IsScalarTower M R R]
    [ContinuousConstSMul M R] : IsScalarTower M C(X, R)‚ÇÄ C(X, R)‚ÇÄ where
  smul_assoc m f g := ext fun x ‚Ü¶ smul_assoc m (f x) (g x)

instance instStarRing [StarRing R] [ContinuousStar R] : StarRing C(X, R)‚ÇÄ where
  star f := ‚ü®star f, by simp‚ü©
  star_involutive _ := ext fun _ ‚Ü¶ star_star _
  star_mul _ _ := ext fun _ ‚Ü¶ star_mul ..
  star_add _ _ := ext fun _ ‚Ü¶ star_add ..

instance instStarModule [StarRing R] {M : Type*} [SMulZeroClass M R] [ContinuousConstSMul M R]
    [Star M] [StarModule M R] [ContinuousStar R] : StarModule M C(X, R)‚ÇÄ where
  star_smul r f := ext fun x ‚Ü¶ star_smul r (f x)

@[simp] lemma coe_star [StarRing R] [ContinuousStar R] (f : C(X, R)‚ÇÄ) : ‚áë(star f) = star ‚áëf := rfl

instance [StarRing R] [ContinuousStar R] [TrivialStar R] : TrivialStar C(X, R)‚ÇÄ where
  star_trivial _ := DFunLike.ext _ _ fun _ ‚Ü¶ star_trivial _

instance instCanLift : CanLift C(X, R) C(X, R)‚ÇÄ (‚Üë) (fun f ‚Ü¶ f 0 = 0) where
  prf f hf := ‚ü®‚ü®f, hf‚ü©, rfl‚ü©

/-- The coercion `C(X, R)‚ÇÄ ‚Üí C(X, R)` bundled as a non-unital star algebra homomorphism. -/
@[simps]
def toContinuousMapHom [StarRing R] [ContinuousStar R] : C(X, R)‚ÇÄ ‚Üí‚ãÜ‚Çô‚Çê[R] C(X, R) where
  toFun f := f
  map_smul' _ _ := rfl
  map_zero' := rfl
  map_add' _ _ := rfl
  map_mul' _ _ := rfl
  map_star' _ := rfl

lemma coe_toContinuousMapHom [StarRing R] [ContinuousStar R] :
    ‚áë(toContinuousMapHom (X := X) (R := R)) = (‚Üë) :=
  rfl

/-- The coercion `C(X, R)‚ÇÄ ‚Üí C(X, R)` bundled as a continuous linear map. -/
@[simps]
def toContinuousMapCLM (M : Type*) [Semiring M] [Module M R] [ContinuousConstSMul M R] :
    C(X, R)‚ÇÄ ‚ÜíL[M] C(X, R) where
  toFun f := f
  map_add' _ _ := rfl
  map_smul' _ _ := rfl

/-- The evaluation at a point, as a continuous linear map from `C(X, R)‚ÇÄ` to `R`. -/
def evalCLM (ùïú : Type*) {R : Type*} [CompactSpace X] [NormedField ùïú] [NormedCommRing R]
    [NormedSpace ùïú R] (x : X) : C(X, R)‚ÇÄ ‚ÜíL[ùïú] R :=
  (ContinuousMap.evalCLM ùïú x).comp (toContinuousMapCLM ùïú : C(X, R)‚ÇÄ ‚ÜíL[ùïú] C(X, R))

@[simp]
lemma evalCLM_apply {ùïú : Type*} {R : Type*} [CompactSpace X] [NormedField ùïú] [NormedCommRing R]
    [NormedSpace ùïú R] (x : X) (f : C(X, R)‚ÇÄ) : evalCLM ùïú x f = f x := rfl

/-- Coercion to a function as an `AddMonoidHom`. Similar to `ContinuousMap.coeFnAddMonoidHom`. -/
def coeFnAddMonoidHom : C(X, R)‚ÇÄ ‚Üí+ X ‚Üí R where
  toFun f := f
  map_zero' := coe_zero
  map_add' f g := by simp

@[simp] lemma coe_sum {Œπ : Type*} (s : Finset Œπ)
    (f : Œπ ‚Üí C(X, R)‚ÇÄ) : ‚áë(s.sum f) = s.sum (fun i => ‚áë(f i)) :=
  map_sum coeFnAddMonoidHom f s

end Semiring

section Ring

variable {X R : Type*} [Zero X] [TopologicalSpace X]
variable [CommRing R] [TopologicalSpace R] [TopologicalRing R]

instance instSub : Sub C(X, R)‚ÇÄ where
  sub f g := ‚ü®f - g, by simp‚ü©

instance instNeg : Neg C(X, R)‚ÇÄ where
  neg f := ‚ü®-f, by simp‚ü©

instance instNonUnitalCommRing : NonUnitalCommRing C(X, R)‚ÇÄ :=
  toContinuousMap_injective.nonUnitalCommRing _ rfl
  (fun _ _ ‚Ü¶ rfl) (fun _ _ ‚Ü¶ rfl) (fun _ ‚Ü¶ rfl) (fun _ _ ‚Ü¶ rfl) (fun _ _ ‚Ü¶ rfl) (fun _ _ ‚Ü¶ rfl)

@[simp]
lemma coe_neg (f : C(X, R)‚ÇÄ) : ‚áë(-f) = -‚áëf := rfl

instance : ContinuousNeg C(X, R)‚ÇÄ where
  continuous_neg := by
    rw [continuous_induced_rng]
    exact continuous_neg.comp continuous_induced_dom

end Ring

end Algebra

section UniformSpace

variable {X R : Type*} [Zero X] [TopologicalSpace X]
variable [Zero R] [UniformSpace R]

protected instance instUniformSpace : UniformSpace C(X, R)‚ÇÄ := .comap toContinuousMap inferInstance

lemma uniformEmbedding_toContinuousMap :
    UniformEmbedding ((‚Üë) : C(X, R)‚ÇÄ ‚Üí C(X, R)) where
  comap_uniformity := rfl
  inj _ _ h := ext fun x ‚Ü¶ congr($(h) x)

instance [T1Space R] [CompleteSpace C(X, R)] : CompleteSpace C(X, R)‚ÇÄ :=
  completeSpace_iff_isComplete_range uniformEmbedding_toContinuousMap.toUniformInducing
    |>.mpr closedEmbedding_toContinuousMap.isClosed_range.isComplete

lemma uniformEmbedding_comp {Y : Type*} [UniformSpace Y] [Zero Y] (g : C(Y, R)‚ÇÄ)
    (hg : UniformEmbedding g) : UniformEmbedding (g.comp ¬∑ : C(X, Y)‚ÇÄ ‚Üí C(X, R)‚ÇÄ) :=
  uniformEmbedding_toContinuousMap.of_comp_iff.mp <|
    ContinuousMap.uniformEmbedding_comp g.toContinuousMap hg |>.comp
      uniformEmbedding_toContinuousMap

/-- The uniform equivalence `C(X, R)‚ÇÄ ‚âÉ·µ§ C(Y, R)‚ÇÄ` induced by a homeomorphism of the domains
sending `0 : X` to `0 : Y`. -/
def _root_.UniformEquiv.arrowCongrLeft‚ÇÄ {Y : Type*} [TopologicalSpace Y] [Zero Y] (f : X ‚âÉ‚Çú Y)
    (hf : f 0 = 0) : C(X, R)‚ÇÄ ‚âÉ·µ§ C(Y, R)‚ÇÄ where
  toFun g := g.comp ‚ü®f.symm.toContinuousMap, (f.toEquiv.apply_eq_iff_eq_symm_apply.eq ‚ñ∏ hf).symm‚ü©
  invFun g := g.comp ‚ü®f.toContinuousMap, hf‚ü©
  left_inv g := ext fun _ ‚Ü¶ congrArg g <| f.left_inv _
  right_inv g := ext fun _ ‚Ü¶ congrArg g <| f.right_inv _
  uniformContinuous_toFun := uniformEmbedding_toContinuousMap.uniformContinuous_iff.mpr <|
    ContinuousMap.uniformContinuous_comp_left f.symm.toContinuousMap |>.comp
    uniformEmbedding_toContinuousMap.uniformContinuous
  uniformContinuous_invFun := uniformEmbedding_toContinuousMap.uniformContinuous_iff.mpr <|
    ContinuousMap.uniformContinuous_comp_left f.toContinuousMap |>.comp
    uniformEmbedding_toContinuousMap.uniformContinuous

end UniformSpace

section CompHoms

variable {X Y M R S : Type*} [Zero X] [Zero Y] [CommSemiring M]
  [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace R] [TopologicalSpace S]
  [CommSemiring R] [StarRing R] [TopologicalSemiring R] [ContinuousStar R]
  [CommSemiring S] [StarRing S] [TopologicalSemiring S] [ContinuousStar S]
  [Module M R] [Module M S] [ContinuousConstSMul M R] [ContinuousConstSMul M S]

variable (R) in
/-- The functor `C(¬∑, R)‚ÇÄ` from topological spaces with zero (and `ContinuousMapZero` maps) to
non-unital star algebras. -/
@[simps]
def nonUnitalStarAlgHom_precomp (f : C(X, Y)‚ÇÄ) : C(Y, R)‚ÇÄ ‚Üí‚ãÜ‚Çô‚Çê[R] C(X, R)‚ÇÄ where
  toFun g := g.comp f
  map_zero' := rfl
  map_add' _ _ := rfl
  map_mul' _ _ := rfl
  map_star' _ := rfl
  map_smul' _ _ := rfl

variable (X) in
/-- The functor `C(X, ¬∑)‚ÇÄ` from non-unital topological star algebras (with non-unital continuous
star homomorphisms) to non-unital star algebras. -/
@[simps apply]
def nonUnitalStarAlgHom_postcomp (œÜ : R ‚Üí‚ãÜ‚Çô‚Çê[M] S) (hœÜ : Continuous œÜ) :
    C(X, R)‚ÇÄ ‚Üí‚ãÜ‚Çô‚Çê[M] C(X, S)‚ÇÄ where
  toFun := .comp ‚ü®‚ü®œÜ, hœÜ‚ü©, by simp‚ü©
  map_zero' := ext <| by simp
  map_add' _ _ := ext <| by simp
  map_mul' _ _ := ext <| by simp
  map_star' _ := ext <| by simp [map_star]
  map_smul' r f := ext <| by simp

end CompHoms

section Norm

variable {Œ± : Type*} {ùïú : Type*} {R : Type*} [TopologicalSpace Œ±] [CompactSpace Œ±] [Zero Œ±]

noncomputable instance [MetricSpace R] [Zero R]: MetricSpace C(Œ±, R)‚ÇÄ :=
  ContinuousMapZero.uniformEmbedding_toContinuousMap.comapMetricSpace _

noncomputable instance [NormedAddCommGroup R] : Norm C(Œ±, R)‚ÇÄ where
  norm f := ‚Äñ(f : C(Œ±, R))‚Äñ

noncomputable instance [NormedCommRing R] : NonUnitalNormedCommRing C(Œ±, R)‚ÇÄ where
  dist_eq f g := NormedAddGroup.dist_eq (f : C(Œ±, R)) g
  norm_mul f g := NormedRing.norm_mul (f : C(Œ±, R)) g
  mul_comm f g := mul_comm f g

instance [NormedField ùïú] [NormedCommRing R] [NormedAlgebra ùïú R] : NormedSpace ùïú C(Œ±, R)‚ÇÄ where
  norm_smul_le r f := norm_smul_le r (f : C(Œ±, R))

instance [NormedCommRing R] [StarRing R] [CStarRing R] : CStarRing C(Œ±, R)‚ÇÄ where
  norm_mul_self_le f := CStarRing.norm_mul_self_le (f : C(Œ±, R))

end Norm

end ContinuousMapZero
