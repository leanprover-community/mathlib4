/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Heather Macbeth
-/
import Mathlib.Topology.ContinuousFunction.Weierstrass
import Mathlib.Data.IsROrC.Basic

#align_import topology.continuous_function.stone_weierstrass from "leanprover-community/mathlib"@"16e59248c0ebafabd5d071b1cd41743eb8698ffb"

/-!
# The Stone-Weierstrass theorem

If a subalgebra `A` of `C(X, â„)`, where `X` is a compact topological space,
separates points, then it is dense.

We argue as follows.

* In any subalgebra `A` of `C(X, â„)`, if `f âˆˆ A`, then `abs f âˆˆ A.topological_closure`.
  This follows from the Weierstrass approximation theorem on `[-â€–fâ€–, â€–fâ€–]` by
  approximating `abs` uniformly thereon by polynomials.
* This ensures that `A.topological_closure` is actually a sublattice:
  if it contains `f` and `g`, then it contains the pointwise supremum `f âŠ” g`
  and the pointwise infimum `f âŠ“ g`.
* Any nonempty sublattice `L` of `C(X, â„)` which separates points is dense,
  by a nice argument approximating a given `f` above and below using separating functions.
  For each `x y : X`, we pick a function `g x y âˆˆ L` so `g x y x = f x` and `g x y y = f y`.
  By continuity these functions remain close to `f` on small patches around `x` and `y`.
  We use compactness to identify a certain finitely indexed infimum of finitely indexed supremums
  which is then close to `f` everywhere, obtaining the desired approximation.
* Finally we put these pieces together. `L = A.topological_closure` is a nonempty sublattice
  which separates points since `A` does, and so is dense (in fact equal to `âŠ¤`).

We then prove the complex version for self-adjoint subalgebras `A`, by separately approximating
the real and imaginary parts using the real subalgebra of real-valued functions in `A`
(which still separates points, by taking the norm-square of a separating function).

## Future work

Extend to cover the case of subalgebras of the continuous functions vanishing at infinity,
on non-compact spaces.

-/


noncomputable section

namespace ContinuousMap

variable {X : Type _} [TopologicalSpace X] [CompactSpace X]

open scoped Polynomial

/-- Turn a function `f : C(X, â„)` into a continuous map into `set.Icc (-â€–fâ€–) (â€–fâ€–)`,
thereby explicitly attaching bounds.
-/
def attachBound (f : C(X, â„)) : C(X, Set.Icc (-â€–fâ€–) â€–fâ€–)
    where toFun x := âŸ¨f x, âŸ¨neg_norm_le_apply f x, apply_le_norm f xâŸ©âŸ©
#align continuous_map.attach_bound ContinuousMap.attachBound

@[simp]
theorem attachBound_apply_coe (f : C(X, â„)) (x : X) : ((attachBound f) x : â„) = f x :=
  rfl
#align continuous_map.attach_bound_apply_coe ContinuousMap.attachBound_apply_coe

theorem polynomial_comp_attachBound (A : Subalgebra â„ C(X, â„)) (f : A) (g : â„[X]) :
    (g.toContinuousMapOn (Set.Icc (-â€–fâ€–) â€–fâ€–)).comp (f : C(X, â„)).attachBound =
      Polynomial.aeval f g := by
  ext
  simp only [ContinuousMap.coe_comp, Function.comp_apply, ContinuousMap.attachBound_apply_coe,
    Polynomial.toContinuousMapOn_apply, Polynomial.aeval_subalgebra_coe,
    Polynomial.aeval_continuousMap_apply, Polynomial.toContinuousMap_apply]
#align continuous_map.polynomial_comp_attach_bound ContinuousMap.polynomial_comp_attachBound

/-- Given a continuous function `f` in a subalgebra of `C(X, â„)`, postcomposing by a polynomial
gives another function in `A`.

This lemma proves something slightly more subtle than this:
we take `f`, and think of it as a function into the restricted target `set.Icc (-â€–fâ€–) â€–fâ€–)`,
and then postcompose with a polynomial function on that interval.
This is in fact the same situation as above, and so also gives a function in `A`.
-/
theorem polynomial_comp_attachBound_mem (A : Subalgebra â„ C(X, â„)) (f : A) (g : â„[X]) :
    (g.toContinuousMapOn (Set.Icc (-â€–fâ€–) â€–fâ€–)).comp (f : C(X, â„)).attachBound âˆˆ A := by
  rw [polynomial_comp_attachBound]
  apply SetLike.coe_mem
#align continuous_map.polynomial_comp_attach_bound_mem ContinuousMap.polynomial_comp_attachBound_mem

theorem comp_attachBound_mem_closure (A : Subalgebra â„ C(X, â„)) (f : A)
    (p : C(Set.Icc (-â€–fâ€–) â€–fâ€–, â„)) : p.comp (attachBound (f : C(X, â„))) âˆˆ A.topologicalClosure := by
  -- `p` itself is in the closure of polynomials, by the Weierstrass theorem,
  have mem_closure : p âˆˆ (polynomialFunctions (Set.Icc (-â€–fâ€–) â€–fâ€–)).topologicalClosure :=
    continuousMap_mem_polynomialFunctions_closure _ _ p
  -- and so there are polynomials arbitrarily close.
  have frequently_mem_polynomials := mem_closure_iff_frequently.mp mem_closure
  -- To prove `p.comp (attached_bound f)` is in the closure of `A`,
  -- we show there are elements of `A` arbitrarily close.
  apply mem_closure_iff_frequently.mpr
  -- To show that, we pull back the polynomials close to `p`,
  refine'
    ((compRightContinuousMap â„ (attachBound (f : C(X, â„)))).continuousAt
            p).tendsto.frequently_map
      _ _ frequently_mem_polynomials
  -- but need to show that those pullbacks are actually in `A`.
  rintro _ âŸ¨g, âŸ¨-, rflâŸ©âŸ©
  simp only [SetLike.mem_coe, AlgHom.coe_toRingHom, compRightContinuousMap_apply,
    Polynomial.toContinuousMapOnAlgHom_apply]
  apply polynomial_comp_attachBound_mem
#align continuous_map.comp_attach_bound_mem_closure ContinuousMap.comp_attachBound_mem_closure

theorem abs_mem_subalgebra_closure (A : Subalgebra â„ C(X, â„)) (f : A) :
    (f : C(X, â„)).abs âˆˆ A.topologicalClosure := by
  let f' := attachBound (f : C(X, â„))
  let abs : C(Set.Icc (-â€–fâ€–) â€–fâ€–, â„) := { toFun := fun x : Set.Icc (-â€–fâ€–) â€–fâ€– => |(x : â„)| }
  change abs.comp f' âˆˆ A.topologicalClosure
  apply comp_attachBound_mem_closure
#align continuous_map.abs_mem_subalgebra_closure ContinuousMap.abs_mem_subalgebra_closure

theorem inf_mem_subalgebra_closure (A : Subalgebra â„ C(X, â„)) (f g : A) :
    (f : C(X, â„)) âŠ“ (g : C(X, â„)) âˆˆ A.topologicalClosure := by
  rw [inf_eq_half_smul_add_sub_abs_sub' â„]
  refine'
    A.topologicalClosure.smul_mem
      (A.topologicalClosure.sub_mem
        (A.topologicalClosure.add_mem (A.le_topologicalClosure f.property)
          (A.le_topologicalClosure g.property))
        _)
      _
  exact_mod_cast abs_mem_subalgebra_closure A _
#align continuous_map.inf_mem_subalgebra_closure ContinuousMap.inf_mem_subalgebra_closure

theorem inf_mem_closed_subalgebra (A : Subalgebra â„ C(X, â„)) (h : IsClosed (A : Set C(X, â„)))
    (f g : A) : (f : C(X, â„)) âŠ“ (g : C(X, â„)) âˆˆ A := by
  convert inf_mem_subalgebra_closure A f g
  apply SetLike.ext'
  symm
  erw [closure_eq_iff_isClosed]
  exact h
#align continuous_map.inf_mem_closed_subalgebra ContinuousMap.inf_mem_closed_subalgebra

theorem sup_mem_subalgebra_closure (A : Subalgebra â„ C(X, â„)) (f g : A) :
    (f : C(X, â„)) âŠ” (g : C(X, â„)) âˆˆ A.topologicalClosure := by
  rw [sup_eq_half_smul_add_add_abs_sub' â„]
  refine'
    A.topologicalClosure.smul_mem
      (A.topologicalClosure.add_mem
        (A.topologicalClosure.add_mem (A.le_topologicalClosure f.property)
          (A.le_topologicalClosure g.property))
        _)
      _
  exact_mod_cast abs_mem_subalgebra_closure A _
#align continuous_map.sup_mem_subalgebra_closure ContinuousMap.sup_mem_subalgebra_closure

theorem sup_mem_closed_subalgebra (A : Subalgebra â„ C(X, â„)) (h : IsClosed (A : Set C(X, â„)))
    (f g : A) : (f : C(X, â„)) âŠ” (g : C(X, â„)) âˆˆ A := by
  convert sup_mem_subalgebra_closure A f g
  apply SetLike.ext'
  symm
  erw [closure_eq_iff_isClosed]
  exact h
#align continuous_map.sup_mem_closed_subalgebra ContinuousMap.sup_mem_closed_subalgebra

open scoped Topology

-- Here's the fun part of Stone-Weierstrass!
theorem sublattice_closure_eq_top (L : Set C(X, â„)) (nA : L.Nonempty)
    (inf_mem : âˆ€ (f) (_ : f âˆˆ L) (g) (_ : g âˆˆ L), f âŠ“ g âˆˆ L)
    (sup_mem : âˆ€ (f) (_ : f âˆˆ L) (g) (_ : g âˆˆ L), f âŠ” g âˆˆ L) (sep : L.SeparatesPointsStrongly) :
    closure L = âŠ¤ := by
  -- We start by boiling down to a statement about close approximation.
  apply eq_top_iff.mpr
  rintro f -
  refine'
    Filter.Frequently.mem_closure
      ((Filter.HasBasis.frequently_iff Metric.nhds_basis_ball).mpr fun Îµ pos => _)
  simp only [exists_prop, Metric.mem_ball]
  -- It will be helpful to assume `X` is nonempty later,
  -- so we get that out of the way here.
  by_cases nX : Nonempty X
  swap
  exact âŸ¨nA.some, (dist_lt_iff pos).mpr fun x => False.elim (nX âŸ¨xâŸ©), nA.choose_specâŸ©
  /-
    The strategy now is to pick a family of continuous functions `g x y` in `A`
    with the property that `g x y x = f x` and `g x y y = f y`
    (this is immediate from `h : separates_points_strongly`)
    then use continuity to see that `g x y` is close to `f` near both `x` and `y`,
    and finally using compactness to produce the desired function `h`
    as a maximum over finitely many `x` of a minimum over finitely many `y` of the `g x y`.
    -/
  dsimp only [Set.SeparatesPointsStrongly] at sep
  choose g hg wâ‚ wâ‚‚ using sep f
  -- For each `x y`, we define `U x y` to be `{z | f z - Îµ < g x y z}`,
  -- and observe this is a neighbourhood of `y`.
  let U : X â†’ X â†’ Set X := fun x y => {z | f z - Îµ < g x y z}
  have U_nhd_y : âˆ€ x y, U x y âˆˆ ğ“ y := by
    intro x y
    refine' IsOpen.mem_nhds _ _
    Â· apply isOpen_lt <;> continuity
    Â· rw [Set.mem_setOf_eq, wâ‚‚]
      exact sub_lt_self _ pos
  -- Fixing `x` for a moment, we have a family of functions `fun y â†¦ g x y`
  -- which on different patches (the `U x y`) are greater than `f z - Îµ`.
  -- Taking the supremum of these functions
  -- indexed by a finite collection of patches which cover `X`
  -- will give us an element of `A` that is globally greater than `f z - Îµ`
  -- and still equal to `f x` at `x`.
  -- Since `X` is compact, for every `x` there is some finset `ys t`
  -- so the union of the `U x y` for `y âˆˆ ys x` still covers everything.
  let ys : âˆ€ _, Finset X := fun x => (CompactSpace.elim_nhds_subcover (U x) (U_nhd_y x)).choose
  let ys_w : âˆ€ x, â‹ƒ y âˆˆ ys x, U x y = âŠ¤ := fun x =>
    (CompactSpace.elim_nhds_subcover (U x) (U_nhd_y x)).choose_spec
  have ys_nonempty : âˆ€ x, (ys x).Nonempty := fun x =>
    Set.nonempty_of_union_eq_top_of_nonempty _ _ nX (ys_w x)
  -- Thus for each `x` we have the desired `h x : A` so `f z - Îµ < h x z` everywhere
  -- and `h x x = f x`.
  let h : âˆ€ _, L := fun x =>
    âŸ¨(ys x).sup' (ys_nonempty x) fun y => (g x y : C(X, â„)),
      Finset.sup'_mem _ sup_mem _ _ _ fun y _ => hg x yâŸ©
  have lt_h : âˆ€ x z, f z - Îµ < (h x : X â†’ â„) z := by
    intro x z
    obtain âŸ¨y, ym, zmâŸ© := Set.exists_set_mem_of_union_eq_top _ _ (ys_w x) z
    dsimp
    simp only [Subtype.coe_mk, sup'_coe, Finset.sup'_apply, Finset.lt_sup'_iff]
    exact âŸ¨y, ym, zmâŸ©
  have h_eq : âˆ€ x, (h x : X â†’ â„) x = f x := by intro x; simp [wâ‚]
  -- For each `x`, we define `W x` to be `{z | h x z < f z + Îµ}`,
  let W : âˆ€ _, Set X := fun x => {z | (h x : X â†’ â„) z < f z + Îµ}
  -- This is still a neighbourhood of `x`.
  have W_nhd : âˆ€ x, W x âˆˆ ğ“ x := by
    intro x
    refine' IsOpen.mem_nhds _ _
    Â· -- Porting note: mathlib3 `continuity` found `continuous_set_coe`
      apply isOpen_lt (continuous_set_coe _ _)
      continuity
    Â· dsimp only [Set.mem_setOf_eq]
      rw [h_eq]
      exact lt_add_of_pos_right _ pos
  -- Since `X` is compact, there is some finset `ys t`
  -- so the union of the `W x` for `x âˆˆ xs` still covers everything.
  let xs : Finset X := (CompactSpace.elim_nhds_subcover W W_nhd).choose
  let xs_w : â‹ƒ x âˆˆ xs, W x = âŠ¤ := (CompactSpace.elim_nhds_subcover W W_nhd).choose_spec
  have xs_nonempty : xs.Nonempty := Set.nonempty_of_union_eq_top_of_nonempty _ _ nX xs_w
  -- Finally our candidate function is the infimum over `x âˆˆ xs` of the `h x`.
  -- This function is then globally less than `f z + Îµ`.
  let k : (L : Type _) :=
    âŸ¨xs.inf' xs_nonempty fun x => (h x : C(X, â„)),
      Finset.inf'_mem _ inf_mem _ _ _ fun x _ => (h x).2âŸ©
  refine' âŸ¨k.1, _, k.2âŸ©
  -- We just need to verify the bound, which we do pointwise.
  rw [dist_lt_iff pos]
  intro z
  -- We rewrite into this particular form,
  -- so that simp lemmas about inequalities involving `finset.inf'` can fire.
  rw [show âˆ€ a b Îµ : â„, dist a b < Îµ â†” a < b + Îµ âˆ§ b - Îµ < a by
        intros; simp only [â† Metric.mem_ball, Real.ball_eq_Ioo, Set.mem_Ioo, and_comm]]
  fconstructor
  Â· dsimp
    simp only [Finset.inf'_lt_iff, ContinuousMap.inf'_apply]
    exact Set.exists_set_mem_of_union_eq_top _ _ xs_w z
  Â· dsimp
    simp only [Finset.lt_inf'_iff, ContinuousMap.inf'_apply]
    rintro x -
    apply lt_h
#align continuous_map.sublattice_closure_eq_top ContinuousMap.sublattice_closure_eq_top

/-- The **Stone-Weierstrass Approximation Theorem**,
that a subalgebra `A` of `C(X, â„)`, where `X` is a compact topological space,
is dense if it separates points.
-/
theorem subalgebra_topologicalClosure_eq_top_of_separatesPoints (A : Subalgebra â„ C(X, â„))
    (w : A.SeparatesPoints) : A.topologicalClosure = âŠ¤ := by
  -- The closure of `A` is closed under taking `sup` and `inf`,
  -- and separates points strongly (since `A` does),
  -- so we can apply `sublattice_closure_eq_top`.
  apply SetLike.ext'
  let L := A.topologicalClosure
  have n : Set.Nonempty (L : Set C(X, â„)) := âŸ¨(1 : C(X, â„)), A.le_topologicalClosure A.one_memâŸ©
  convert
    sublattice_closure_eq_top (L : Set C(X, â„)) n
      (fun f fm g gm => inf_mem_closed_subalgebra L A.isClosed_topologicalClosure âŸ¨f, fmâŸ© âŸ¨g, gmâŸ©)
      (fun f fm g gm => sup_mem_closed_subalgebra L A.isClosed_topologicalClosure âŸ¨f, fmâŸ© âŸ¨g, gmâŸ©)
      (Subalgebra.SeparatesPoints.strongly
        (Subalgebra.separatesPoints_monotone A.le_topologicalClosure w))
  Â· simp
#align continuous_map.subalgebra_topological_closure_eq_top_of_separates_points ContinuousMap.subalgebra_topologicalClosure_eq_top_of_separatesPoints

/-- An alternative statement of the Stone-Weierstrass theorem.

If `A` is a subalgebra of `C(X, â„)` which separates points (and `X` is compact),
every real-valued continuous function on `X` is a uniform limit of elements of `A`.
-/
theorem continuousMap_mem_subalgebra_closure_of_separatesPoints (A : Subalgebra â„ C(X, â„))
    (w : A.SeparatesPoints) (f : C(X, â„)) : f âˆˆ A.topologicalClosure := by
  rw [subalgebra_topologicalClosure_eq_top_of_separatesPoints A w]
  simp
#align continuous_map.continuous_map_mem_subalgebra_closure_of_separates_points ContinuousMap.continuousMap_mem_subalgebra_closure_of_separatesPoints

/-- An alternative statement of the Stone-Weierstrass theorem,
for those who like their epsilons.

If `A` is a subalgebra of `C(X, â„)` which separates points (and `X` is compact),
every real-valued continuous function on `X` is within any `Îµ > 0` of some element of `A`.
-/
theorem exists_mem_subalgebra_near_continuousMap_of_separatesPoints (A : Subalgebra â„ C(X, â„))
    (w : A.SeparatesPoints) (f : C(X, â„)) (Îµ : â„) (pos : 0 < Îµ) :
    âˆƒ g : A, â€–(g : C(X, â„)) - fâ€– < Îµ := by
  have w :=
    mem_closure_iff_frequently.mp (continuousMap_mem_subalgebra_closure_of_separatesPoints A w f)
  rw [Metric.nhds_basis_ball.frequently_iff] at w
  obtain âŸ¨g, H, mâŸ© := w Îµ pos
  rw [Metric.mem_ball, dist_eq_norm] at H
  exact âŸ¨âŸ¨g, mâŸ©, HâŸ©
#align continuous_map.exists_mem_subalgebra_near_continuous_map_of_separates_points ContinuousMap.exists_mem_subalgebra_near_continuousMap_of_separatesPoints

/-- An alternative statement of the Stone-Weierstrass theorem,
for those who like their epsilons and don't like bundled continuous functions.

If `A` is a subalgebra of `C(X, â„)` which separates points (and `X` is compact),
every real-valued continuous function on `X` is within any `Îµ > 0` of some element of `A`.
-/
theorem exists_mem_subalgebra_near_continuous_of_separatesPoints (A : Subalgebra â„ C(X, â„))
    (w : A.SeparatesPoints) (f : X â†’ â„) (c : Continuous f) (Îµ : â„) (pos : 0 < Îµ) :
    âˆƒ g : A, âˆ€ x, â€–(g : X â†’ â„) x - f xâ€– < Îµ := by
  obtain âŸ¨g, bâŸ© := exists_mem_subalgebra_near_continuousMap_of_separatesPoints A w âŸ¨f, câŸ© Îµ pos
  use g
  rwa [norm_lt_iff _ pos] at b
#align continuous_map.exists_mem_subalgebra_near_continuous_of_separates_points ContinuousMap.exists_mem_subalgebra_near_continuous_of_separatesPoints

end ContinuousMap

section IsROrC

open IsROrC

-- Redefine `X`, since for the next few lemmas it need not be compact
variable {ğ•œ : Type _} {X : Type _} [IsROrC ğ•œ] [TopologicalSpace X]

namespace ContinuousMap

/-- A real subalgebra of `C(X, ğ•œ)` is `conj_invariant`, if it contains all its conjugates. -/
def ConjInvariantSubalgebra (A : Subalgebra â„ C(X, ğ•œ)) : Prop :=
  A.map (conjAe.toAlgHom.compLeftContinuous â„ conjCle.continuous) â‰¤ A
#align continuous_map.conj_invariant_subalgebra ContinuousMap.ConjInvariantSubalgebra

theorem mem_conjInvariantSubalgebra {A : Subalgebra â„ C(X, ğ•œ)} (hA : ConjInvariantSubalgebra A)
    {f : C(X, ğ•œ)} (hf : f âˆˆ A) : (conjAe.toAlgHom.compLeftContinuous â„ conjCle.continuous) f âˆˆ A :=
  hA âŸ¨f, hf, rflâŸ©
#align continuous_map.mem_conj_invariant_subalgebra ContinuousMap.mem_conjInvariantSubalgebra

/-- If a set `S` is conjugation-invariant, then its `ğ•œ`-span is conjugation-invariant. -/
theorem subalgebraConjInvariant {S : Set C(X, ğ•œ)}
    (hS : âˆ€ f, f âˆˆ S â†’ (conjAe.toAlgHom.compLeftContinuous â„ conjCle.continuous) f âˆˆ S) :
    ConjInvariantSubalgebra ((Algebra.adjoin ğ•œ S).restrictScalars â„) := by
  rintro _ âŸ¨f, hf, rflâŸ©
  change _ âˆˆ (Algebra.adjoin ğ•œ S).restrictScalars â„
  change _ âˆˆ (Algebra.adjoin ğ•œ S).restrictScalars â„ at hf
  rw [Subalgebra.mem_restrictScalars] at hf âŠ¢
  refine Algebra.adjoin_induction hf ?_ ?_ ?_ ?_
  Â· exact fun g hg => Algebra.subset_adjoin (hS g hg)
  Â· exact fun c => Subalgebra.algebraMap_mem _ (starRingEnd ğ•œ c)
  Â· intro f g hf hg
    convert Subalgebra.add_mem _ hf hg using 1
    simp only [AlgEquiv.toAlgHom_eq_coe, map_add, RingHom.coe_coe]
  Â· intro f g hf hg
    convert Subalgebra.mul_mem _ hf hg using 1
    simp only [AlgEquiv.toAlgHom_eq_coe, map_mul, RingHom.coe_coe]
#align continuous_map.subalgebra_conj_invariant ContinuousMap.subalgebraConjInvariant

end ContinuousMap

open ContinuousMap

/-- If a conjugation-invariant subalgebra of `C(X, ğ•œ)` separates points, then the real subalgebra
of its purely real-valued elements also separates points. -/
theorem Subalgebra.SeparatesPoints.isROrC_to_real {A : Subalgebra ğ•œ C(X, ğ•œ)}
    (hA : A.SeparatesPoints) (hA' : ConjInvariantSubalgebra (A.restrictScalars â„)) :
    ((A.restrictScalars â„).comap
        (ofRealAm.compLeftContinuous â„ continuous_ofReal)).SeparatesPoints := by
  intro xâ‚ xâ‚‚ hx
  -- Let `f` in the subalgebra `A` separate the points `xâ‚`, `xâ‚‚`
  obtain âŸ¨_, âŸ¨f, hfA, rflâŸ©, hfâŸ© := hA hx
  let F : C(X, ğ•œ) := f - const _ (f xâ‚‚)
  -- Subtract the constant `f xâ‚‚` from `f`; this is still an element of the subalgebra
  have hFA : F âˆˆ A := by
    refine' A.sub_mem hfA (@Eq.subst _ (Â· âˆˆ A) _ _ _ <| A.smul_mem A.one_mem <| f xâ‚‚)
    ext1
    simp only [coe_smul, coe_one, smul_apply, one_apply, Algebra.id.smul_eq_mul, mul_one,
      const_apply]
  -- Consider now the function `fun x â†¦ |f x - f xâ‚‚| ^ 2`
  refine' âŸ¨_, âŸ¨(âŸ¨IsROrC.normSq, continuous_normSqâŸ© : C(ğ•œ, â„)).comp F, _, rflâŸ©, _âŸ©
  Â· -- This is also an element of the subalgebra, and takes only real values
    rw [SetLike.mem_coe, Subalgebra.mem_comap]
    convert (A.restrictScalars â„).mul_mem (mem_conjInvariantSubalgebra hA' hFA) hFA
    ext1
    rw [mul_comm]
    exact (IsROrC.mul_conj _).symm
  Â· -- And it also separates the points `xâ‚`, `xâ‚‚`
    have : f xâ‚ - f xâ‚‚ â‰  0 := sub_ne_zero.mpr hf
    simpa only [comp_apply, coe_sub, coe_const, sub_apply, coe_mk, sub_self, map_zero, Ne.def,
      normSq_eq_zero, const_apply] using this
#align subalgebra.separates_points.is_R_or_C_to_real Subalgebra.SeparatesPoints.isROrC_to_real

variable [CompactSpace X]

/-- The Stone-Weierstrass approximation theorem, `is_R_or_C` version,
that a subalgebra `A` of `C(X, ğ•œ)`, where `X` is a compact topological space and `is_R_or_C ğ•œ`,
is dense if it is conjugation-invariant and separates points.
-/
theorem ContinuousMap.subalgebra_isROrC_topologicalClosure_eq_top_of_separatesPoints
    (A : Subalgebra ğ•œ C(X, ğ•œ)) (hA : A.SeparatesPoints)
    (hA' : ConjInvariantSubalgebra (A.restrictScalars â„)) : A.topologicalClosure = âŠ¤ := by
  rw [Algebra.eq_top_iff]
  -- Let `I` be the natural inclusion of `C(X, â„)` into `C(X, ğ•œ)`
  let I : C(X, â„) â†’â‚—[â„] C(X, ğ•œ) := ofRealClm.compLeftContinuous â„ X
  -- The main point of the proof is that its range (i.e., every real-valued function) is contained
  -- in the closure of `A`
  have key : LinearMap.range I â‰¤ (A.toSubmodule.restrictScalars â„).topologicalClosure := by
    -- Let `Aâ‚€` be the subalgebra of `C(X, â„)` consisting of `A`'s purely real elements; it is the
    -- preimage of `A` under `I`.  In this argument we only need its submodule structure.
    let Aâ‚€ : Submodule â„ C(X, â„) := (A.toSubmodule.restrictScalars â„).comap I
    -- By `subalgebra.separates_points.complex_to_real`, this subalgebra also separates points, so
    -- we may apply the real Stone-Weierstrass result to it.
    have SW : Aâ‚€.topologicalClosure = âŠ¤ :=
      haveI :=
        subalgebra_topologicalClosure_eq_top_of_separatesPoints _ (hA.isROrC_to_real hA')
      congr_arg Subalgebra.toSubmodule this
    rw [â† Submodule.map_top, â† SW]
    -- So it suffices to prove that the image under `I` of the closure of `Aâ‚€` is contained in the
    -- closure of `A`, which follows by abstract nonsense
    have hâ‚ := Aâ‚€.topologicalClosure_map ((@ofRealClm ğ•œ _).compLeftContinuousCompact X)
    have hâ‚‚ := (A.toSubmodule.restrictScalars â„).map_comap_le I
    exact hâ‚.trans (Submodule.topologicalClosure_mono hâ‚‚)
  -- In particular, for a function `f` in `C(X, ğ•œ)`, the real and imaginary parts of `f` are in the
  -- closure of `A`
  intro f
  let f_re : C(X, â„) := (âŸ¨IsROrC.re, IsROrC.reClm.continuousâŸ© : C(ğ•œ, â„)).comp f
  let f_im : C(X, â„) := (âŸ¨IsROrC.im, IsROrC.imClm.continuousâŸ© : C(ğ•œ, â„)).comp f
  have h_f_re : I f_re âˆˆ A.topologicalClosure := key âŸ¨f_re, rflâŸ©
  have h_f_im : I f_im âˆˆ A.topologicalClosure := key âŸ¨f_im, rflâŸ©
  -- So `f_re + I â€¢ f_im` is in the closure of `A`
  convert A.topologicalClosure.add_mem h_f_re (A.topologicalClosure.smul_mem h_f_im IsROrC.I)
  -- And this, of course, is just `f`
  ext
  apply Eq.symm
  simp [mul_comm IsROrC.I _]
#align continuous_map.subalgebra_is_R_or_C_topological_closure_eq_top_of_separates_points ContinuousMap.subalgebra_isROrC_topologicalClosure_eq_top_of_separatesPoints

end IsROrC
