import Mathlib

section UniqueUnital

section IsROrC

variable {ğ•œ A : Type*} [IsROrC ğ•œ] [NormedRing A] [StarRing A] [NormedAlgebra ğ•œ A] [CompleteSpace A]
instance : UniqueContinuousFunctionalCalculus ğ•œ A where
  eq_of_continuous_of_map_id s hs Ï† Ïˆ hÏ† hÏˆ h :=
    ContinuousMap.starAlgHom_ext_map_X hÏ† hÏˆ <| by
      convert h using 1
      all_goals exact congr_arg _ (by ext; simp)
  compactSpace_spectrum a := inferInstance

end IsROrC

section NNReal
open NNReal

variable {A : Type*} [NormedRing A] [StarRing A] [NormedAlgebra â„ A] [CompleteSpace A]
variable {X : Type*} [TopologicalSpace X]

lemma max_neg_zero {Î± : Type*} [AddCommGroup Î±] [LinearOrder Î±] [CovariantClass Î± Î± (Â· + Â·) (Â· â‰¤ Â·)]
    (a : Î±) : max (-a) 0 = max a 0 - a := by
  have := congr(-$(max_zero_sub_eq_self a))
  rwa [neg_sub, sub_eq_iff_eq_add', â† sub_eq_add_neg] at this

/-- This map sends `f : C(X, â„)` to `f âŠ” 0`, bundled as a continuous map `C(X, â„â‰¥0)`. -/
@[pp_dot]
noncomputable def ContinuousMap.toNNReal (f : C(X, â„)) : C(X, â„â‰¥0) :=
  .realToNNReal |>.comp f

@[fun_prop]
lemma ContinuousMap.continuous_toNNReal : Continuous (ContinuousMap.toNNReal (X := X)) :=
  ContinuousMap.continuous_comp _

@[simp]
lemma ContinuousMap.toNNReal_apply (f : C(X, â„)) (x : X) : f.toNNReal x = (f x).toNNReal := rfl

lemma ContinuousMap.toNNReal_add (f g : C(X, â„)) :
    (f + g).toNNReal + (-f).toNNReal + (-g).toNNReal =
      (-(f + g)).toNNReal + f.toNNReal + g.toNNReal := by
  ext x
  simp [max_neg_zero, -neg_add_rev]
  abel

lemma ContinuousMap.toNNReal_mul (f g : C(X, â„)) :
    (f * g).toNNReal + (-f).toNNReal * g.toNNReal + f.toNNReal * (-g).toNNReal=
      (-(f * g)).toNNReal + f.toNNReal * g.toNNReal + (-f).toNNReal * (-g).toNNReal := by
  ext x
  simp [max_neg_zero, mul_sub, sub_mul]
  abel

@[simp]
lemma ContinuousMap.toNNReal_algebraMap (r : â„â‰¥0) :
    (algebraMap â„ C(X, â„) r).toNNReal = algebraMap â„â‰¥0 C(X, â„â‰¥0) r := by
  ext; simp

@[simp]
lemma ContinuousMap.toNNReal_neg_algebraMap (r : â„â‰¥0) :
    (- algebraMap â„ C(X, â„) r).toNNReal = 0 := by
  ext; simp

/-- Given a star `â„â‰¥0`-algebra homomorphism `Ï†` from `C(X, â„â‰¥0)` into an `â„`-algebra `A`, this is
the unique extension of `Ï†` from `C(X, â„)` to `A` as a star `â„`-algebra homomorphism. -/
@[simps]
noncomputable def StarAlgHom.realContinuousMapOfNNReal (Ï† : C(X, â„â‰¥0) â†’â‹†â‚[â„â‰¥0] A) :
    C(X, â„) â†’â‹†â‚[â„] A where
  toFun f := Ï† f.toNNReal - Ï† (-f).toNNReal
  map_one' := by
    have hâ‚ : (1 : C(X, â„)).toNNReal = 1 := ?_
    have hâ‚‚ : (-1 : C(X, â„)).toNNReal = 0 := ?_
    Â· simp only [hâ‚, map_one Ï†, hâ‚‚, map_zero Ï†, sub_zero]
    all_goals ext x; simp
  map_zero' := by simp only [neg_zero, sub_self]
  map_mul' f g := by
    have := congr(Ï† $(f.toNNReal_mul g))
    simp only [map_add, map_mul, sub_mul, mul_sub] at this âŠ¢
    rw [â† sub_eq_zero] at this âŠ¢
    convert this using 1
    abel
  map_add' f g := by
    have := congr(Ï† $(f.toNNReal_add g))
    simp only [map_add] at this âŠ¢
    rw [â† sub_eq_zero] at this âŠ¢
    convert this using 1
    abel
  commutes' r := by
    simp only
    obtain (hr | hr) := le_total 0 r
    Â· lift r to â„â‰¥0 using hr
      simpa only [ContinuousMap.toNNReal_algebraMap, ContinuousMap.toNNReal_neg_algebraMap,
        map_zero, sub_zero] using AlgHomClass.commutes Ï† r
    Â· rw [â† neg_neg r, â† map_neg, neg_neg (-r)]
      rw [â† neg_nonneg] at hr
      lift -r to â„â‰¥0 using hr with r
      simpa only [map_neg, ContinuousMap.toNNReal_neg_algebraMap, map_zero,
        ContinuousMap.toNNReal_algebraMap, zero_sub, neg_inj] using AlgHomClass.commutes Ï† r
  map_star' f := by simp only [star_trivial, star_sub, â† map_star]

@[fun_prop]
lemma StarAlgHom.continuous_realContinuousMapOfNNReal (Ï† : C(X, â„â‰¥0) â†’â‹†â‚[â„â‰¥0] A)
    (hÏ† : Continuous Ï†) : Continuous Ï†.realContinuousMapOfNNReal := by
  simp [realContinuousMapOfNNReal]
  fun_prop

@[simp high]
lemma StarAlgHom.realContinuousMapOfNNReal_apply_comp_toReal (Ï† : C(X, â„â‰¥0) â†’â‹†â‚[â„â‰¥0] A)
    (f : C(X, â„â‰¥0)) :
    Ï†.realContinuousMapOfNNReal ((ContinuousMap.mk toReal continuous_coe).comp f) = Ï† f := by
  simp only [realContinuousMapOfNNReal_apply]
  convert_to Ï† f - Ï† 0 = Ï† f using 2
  on_goal -1 => rw [map_zero, sub_zero]
  all_goals
    congr
    ext x
    simp

lemma StarAlgHom.injective_realContinuousMapOfNNReal :
    Function.Injective (realContinuousMapOfNNReal (X := X) (A := A)) := by
  intro Ï† Ïˆ h
  ext f
  simpa using congr($(h) ((ContinuousMap.mk toReal continuous_coe).comp f))

attribute [pp_dot] ContinuousMap.comp

instance : UniqueContinuousFunctionalCalculus â„â‰¥0 A where
  compactSpace_spectrum := inferInstance
  eq_of_continuous_of_map_id s hs Ï† Ïˆ hÏ† hÏˆ h := by
    let s' : Set â„ := (â†‘) '' s
    let e : s â‰ƒâ‚œ s' :=
      { toFun := Subtype.map (â†‘) (by simp [s'])
        invFun := Subtype.map Real.toNNReal (by simp [s'])
        left_inv := fun _ â†¦ by ext; simp
        right_inv := fun x â†¦ by
          ext
          obtain âŸ¨y, -, hyâŸ© := x.2
          simpa using hy â–¸ NNReal.coe_nonneg y
        continuous_toFun := continuous_coe.subtype_map (by simp [s'])
        continuous_invFun := continuous_real_toNNReal.subtype_map (by simp [s']) }
    have (Î¾ : C(s, â„â‰¥0) â†’â‹†â‚[â„â‰¥0] A) (hÎ¾ : Continuous Î¾) :
        (let Î¾' := Î¾.realContinuousMapOfNNReal.comp <| ContinuousMap.compStarAlgHom' â„ â„ e;
        Continuous Î¾' âˆ§ Î¾' (.restrict s' <| .id â„) = Î¾ (.restrict s <| .id â„â‰¥0)) := by
      intro Î¾'
      refine âŸ¨Î¾.continuous_realContinuousMapOfNNReal hÎ¾ |>.comp <|
        ContinuousMap.continuous_comp_left _, ?_âŸ©
      exact Î¾.realContinuousMapOfNNReal_apply_comp_toReal (.restrict s <| .id â„â‰¥0)
    obtain âŸ¨hÏ†', hÏ†_idâŸ© := this Ï† hÏ†
    obtain âŸ¨hÏˆ', hÏˆ_idâŸ© := this Ïˆ hÏˆ
    have hs' : CompactSpace s' := e.compactSpace
    have h' := UniqueContinuousFunctionalCalculus.eq_of_continuous_of_map_id s' _ _ hÏ†' hÏˆ'
      (hÏ†_id â–¸ hÏˆ_id â–¸ h)
    have h'' := congr($(h').comp <| ContinuousMap.compStarAlgHom' â„ â„ (e.symm : C(s', s)))
    have : (ContinuousMap.compStarAlgHom' â„ â„ (e : C(s, s'))).comp
        (ContinuousMap.compStarAlgHom' â„ â„ (e.symm : C(s', s))) = StarAlgHom.id _ _ := by
      ext1; simp
    simp only [StarAlgHom.comp_assoc, this, StarAlgHom.comp_id] at h''
    exact StarAlgHom.injective_realContinuousMapOfNNReal h''

end NNReal

end UniqueUnital
