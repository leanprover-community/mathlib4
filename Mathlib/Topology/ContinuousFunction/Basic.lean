/-
Copyright (c) 2020 Nicol√≤ Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Nicol√≤ Cavalleri
-/
import Mathlib.Data.Set.UnionLift
import Mathlib.Topology.Homeomorph

#align_import topology.continuous_function.basic from "leanprover-community/mathlib"@"55d771df074d0dd020139ee1cd4b95521422df9f"

/-!
# Continuous bundled maps

In this file we define the type `ContinuousMap` of continuous bundled maps.

We use the `DFunLike` design, so each type of morphisms has a companion typeclass which is meant to
be satisfied by itself and all stricter types.
-/


open Function
open scoped Topology

/-- The type of continuous maps from `Œ±` to `Œ≤`.

When possible, instead of parametrizing results over `(f : C(Œ±, Œ≤))`,
you should parametrize over `{F : Type*} [ContinuousMapClass F Œ± Œ≤] (f : F)`.

When you extend this structure, make sure to extend `ContinuousMapClass`. -/
structure ContinuousMap (Œ± Œ≤ : Type*) [TopologicalSpace Œ±] [TopologicalSpace Œ≤] where
  /-- The function `Œ± ‚Üí Œ≤` -/
  protected toFun : Œ± ‚Üí Œ≤
  /-- Proposition that `toFun` is continuous -/
  protected continuous_toFun : Continuous toFun := by continuity
#align continuous_map ContinuousMap

/-- The type of continuous maps from `Œ±` to `Œ≤`. -/
notation "C(" Œ± ", " Œ≤ ")" => ContinuousMap Œ± Œ≤

section

/-- `ContinuousMapClass F Œ± Œ≤` states that `F` is a type of continuous maps.

You should extend this class when you extend `ContinuousMap`. -/
class ContinuousMapClass (F Œ± Œ≤ : Type*) [TopologicalSpace Œ±] [TopologicalSpace Œ≤]
    [FunLike F Œ± Œ≤] : Prop where
  /-- Continuity -/
  map_continuous (f : F) : Continuous f
#align continuous_map_class ContinuousMapClass

end

export ContinuousMapClass (map_continuous)

attribute [continuity, fun_prop] map_continuous

section ContinuousMapClass

variable {F Œ± Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [FunLike F Œ± Œ≤]
variable [ContinuousMapClass F Œ± Œ≤]

theorem map_continuousAt (f : F) (a : Œ±) : ContinuousAt f a :=
  (map_continuous f).continuousAt
#align map_continuous_at map_continuousAt

theorem map_continuousWithinAt (f : F) (s : Set Œ±) (a : Œ±) : ContinuousWithinAt f s a :=
  (map_continuous f).continuousWithinAt
#align map_continuous_within_at map_continuousWithinAt

/-- Coerce a bundled morphism with a `ContinuousMapClass` instance to a `ContinuousMap`. -/
@[coe] def toContinuousMap (f : F) : C(Œ±, Œ≤) := ‚ü®f, map_continuous f‚ü©

instance : CoeTC F C(Œ±, Œ≤) := ‚ü®toContinuousMap‚ü©

end ContinuousMapClass

/-! ### Continuous maps-/


namespace ContinuousMap

variable {Œ± Œ≤ Œ≥ Œ¥ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [TopologicalSpace Œ≥]
  [TopologicalSpace Œ¥]

instance funLike : FunLike C(Œ±, Œ≤) Œ± Œ≤ where
  coe := ContinuousMap.toFun
  coe_injective' f g h := by cases f; cases g; congr

instance toContinuousMapClass : ContinuousMapClass C(Œ±, Œ≤) Œ± Œ≤ where
  map_continuous := ContinuousMap.continuous_toFun

@[simp]
theorem toFun_eq_coe {f : C(Œ±, Œ≤)} : f.toFun = (f : Œ± ‚Üí Œ≤) :=
  rfl
#align continuous_map.to_fun_eq_coe ContinuousMap.toFun_eq_coe

instance : CanLift (Œ± ‚Üí Œ≤) C(Œ±, Œ≤) DFunLike.coe Continuous := ‚ü®fun f hf ‚Ü¶ ‚ü®‚ü®f, hf‚ü©, rfl‚ü©‚ü©

/-- See note [custom simps projection]. -/
def Simps.apply (f : C(Œ±, Œ≤)) : Œ± ‚Üí Œ≤ := f

-- this must come after the coe_to_fun definition
initialize_simps_projections ContinuousMap (toFun ‚Üí apply)

@[simp] -- Porting note: removed `norm_cast` attribute
protected theorem coe_coe {F : Type*} [FunLike F Œ± Œ≤] [ContinuousMapClass F Œ± Œ≤] (f : F) :
    ‚áë(f : C(Œ±, Œ≤)) = f :=
  rfl
#align continuous_map.coe_coe ContinuousMap.coe_coe

@[ext]
theorem ext {f g : C(Œ±, Œ≤)} (h : ‚àÄ a, f a = g a) : f = g :=
  DFunLike.ext _ _ h
#align continuous_map.ext ContinuousMap.ext

/-- Copy of a `ContinuousMap` with a new `toFun` equal to the old one. Useful to fix definitional
equalities. -/
protected def copy (f : C(Œ±, Œ≤)) (f' : Œ± ‚Üí Œ≤) (h : f' = f) : C(Œ±, Œ≤) where
  toFun := f'
  continuous_toFun := h.symm ‚ñ∏ f.continuous_toFun
#align continuous_map.copy ContinuousMap.copy

@[simp]
theorem coe_copy (f : C(Œ±, Œ≤)) (f' : Œ± ‚Üí Œ≤) (h : f' = f) : ‚áë(f.copy f' h) = f' :=
  rfl
#align continuous_map.coe_copy ContinuousMap.coe_copy

theorem copy_eq (f : C(Œ±, Œ≤)) (f' : Œ± ‚Üí Œ≤) (h : f' = f) : f.copy f' h = f :=
  DFunLike.ext' h
#align continuous_map.copy_eq ContinuousMap.copy_eq

variable {f g : C(Œ±, Œ≤)}

/-- Deprecated. Use `map_continuous` instead. -/
protected theorem continuous (f : C(Œ±, Œ≤)) : Continuous f :=
  f.continuous_toFun
#align continuous_map.continuous ContinuousMap.continuous

@[continuity]
theorem continuous_set_coe (s : Set C(Œ±, Œ≤)) (f : s) : Continuous (f : Œ± ‚Üí Œ≤) :=
  f.1.continuous
#align continuous_map.continuous_set_coe ContinuousMap.continuous_set_coe

/-- Deprecated. Use `map_continuousAt` instead. -/
protected theorem continuousAt (f : C(Œ±, Œ≤)) (x : Œ±) : ContinuousAt f x :=
  f.continuous.continuousAt
#align continuous_map.continuous_at ContinuousMap.continuousAt

/-- Deprecated. Use `DFunLike.congr_fun` instead. -/
protected theorem congr_fun {f g : C(Œ±, Œ≤)} (H : f = g) (x : Œ±) : f x = g x :=
  H ‚ñ∏ rfl
#align continuous_map.congr_fun ContinuousMap.congr_fun

/-- Deprecated. Use `DFunLike.congr_arg` instead. -/
protected theorem congr_arg (f : C(Œ±, Œ≤)) {x y : Œ±} (h : x = y) : f x = f y :=
  h ‚ñ∏ rfl
#align continuous_map.congr_arg ContinuousMap.congr_arg

theorem coe_injective : @Function.Injective C(Œ±, Œ≤) (Œ± ‚Üí Œ≤) (‚Üë) := fun f g h => by
  cases f; cases g; congr
#align continuous_map.coe_injective ContinuousMap.coe_injective

@[simp]
theorem coe_mk (f : Œ± ‚Üí Œ≤) (h : Continuous f) : ‚áë(‚ü®f, h‚ü© : C(Œ±, Œ≤)) = f :=
  rfl
#align continuous_map.coe_mk ContinuousMap.coe_mk

theorem map_specializes (f : C(Œ±, Œ≤)) {x y : Œ±} (h : x ‚§≥ y) : f x ‚§≥ f y :=
  h.map f.2
#align continuous_map.map_specializes ContinuousMap.map_specializes

section

variable (Œ± Œ≤)

/--
The continuous functions from `Œ±` to `Œ≤` are the same as the plain functions when `Œ±` is discrete.
-/
@[simps]
def equivFnOfDiscrete [DiscreteTopology Œ±] : C(Œ±, Œ≤) ‚âÉ (Œ± ‚Üí Œ≤) :=
  ‚ü®fun f => f,
    fun f => ‚ü®f, continuous_of_discreteTopology‚ü©,
    fun _ => by ext; rfl,
    fun _ => by ext; rfl‚ü©
#align continuous_map.equiv_fn_of_discrete ContinuousMap.equivFnOfDiscrete

end

variable (Œ±)

/-- The identity as a continuous map. -/
protected def id : C(Œ±, Œ±) where
  toFun := id
#align continuous_map.id ContinuousMap.id

@[simp]
theorem coe_id : ‚áë(ContinuousMap.id Œ±) = id :=
  rfl
#align continuous_map.coe_id ContinuousMap.coe_id

/-- The constant map as a continuous map. -/
def const (b : Œ≤) : C(Œ±, Œ≤) where
  toFun := fun _ : Œ± => b
#align continuous_map.const ContinuousMap.const

@[simp]
theorem coe_const (b : Œ≤) : ‚áë(const Œ± b) = Function.const Œ± b :=
  rfl
#align continuous_map.coe_const ContinuousMap.coe_const

/-- `Function.const Œ± b` as a bundled continuous function of `b`. -/
@[simps (config := .asFn)]
def constPi : C(Œ≤, Œ± ‚Üí Œ≤) where
  toFun b := Function.const Œ± b

instance [Inhabited Œ≤] : Inhabited C(Œ±, Œ≤) :=
  ‚ü®const Œ± default‚ü©

variable {Œ±}

@[simp]
theorem id_apply (a : Œ±) : ContinuousMap.id Œ± a = a :=
  rfl
#align continuous_map.id_apply ContinuousMap.id_apply

@[simp]
theorem const_apply (b : Œ≤) (a : Œ±) : const Œ± b a = b :=
  rfl
#align continuous_map.const_apply ContinuousMap.const_apply

/-- The composition of continuous maps, as a continuous map. -/
def comp (f : C(Œ≤, Œ≥)) (g : C(Œ±, Œ≤)) : C(Œ±, Œ≥) where
  toFun := f ‚àò g
#align continuous_map.comp ContinuousMap.comp

@[simp]
theorem coe_comp (f : C(Œ≤, Œ≥)) (g : C(Œ±, Œ≤)) : ‚áë(comp f g) = f ‚àò g :=
  rfl
#align continuous_map.coe_comp ContinuousMap.coe_comp

@[simp]
theorem comp_apply (f : C(Œ≤, Œ≥)) (g : C(Œ±, Œ≤)) (a : Œ±) : comp f g a = f (g a) :=
  rfl
#align continuous_map.comp_apply ContinuousMap.comp_apply

@[simp]
theorem comp_assoc (f : C(Œ≥, Œ¥)) (g : C(Œ≤, Œ≥)) (h : C(Œ±, Œ≤)) :
    (f.comp g).comp h = f.comp (g.comp h) :=
  rfl
#align continuous_map.comp_assoc ContinuousMap.comp_assoc

@[simp]
theorem id_comp (f : C(Œ±, Œ≤)) : (ContinuousMap.id _).comp f = f :=
  ext fun _ => rfl
#align continuous_map.id_comp ContinuousMap.id_comp

@[simp]
theorem comp_id (f : C(Œ±, Œ≤)) : f.comp (ContinuousMap.id _) = f :=
  ext fun _ => rfl
#align continuous_map.comp_id ContinuousMap.comp_id

@[simp]
theorem const_comp (c : Œ≥) (f : C(Œ±, Œ≤)) : (const Œ≤ c).comp f = const Œ± c :=
  ext fun _ => rfl
#align continuous_map.const_comp ContinuousMap.const_comp

@[simp]
theorem comp_const (f : C(Œ≤, Œ≥)) (b : Œ≤) : f.comp (const Œ± b) = const Œ± (f b) :=
  ext fun _ => rfl
#align continuous_map.comp_const ContinuousMap.comp_const

@[simp]
theorem cancel_right {f‚ÇÅ f‚ÇÇ : C(Œ≤, Œ≥)} {g : C(Œ±, Œ≤)} (hg : Surjective g) :
    f‚ÇÅ.comp g = f‚ÇÇ.comp g ‚Üî f‚ÇÅ = f‚ÇÇ :=
  ‚ü®fun h => ext <| hg.forall.2 <| DFunLike.ext_iff.1 h, congr_arg (ContinuousMap.comp ¬∑ g)‚ü©
#align continuous_map.cancel_right ContinuousMap.cancel_right

@[simp]
theorem cancel_left {f : C(Œ≤, Œ≥)} {g‚ÇÅ g‚ÇÇ : C(Œ±, Œ≤)} (hf : Injective f) :
    f.comp g‚ÇÅ = f.comp g‚ÇÇ ‚Üî g‚ÇÅ = g‚ÇÇ :=
  ‚ü®fun h => ext fun a => hf <| by rw [‚Üê comp_apply, h, comp_apply], congr_arg _‚ü©
#align continuous_map.cancel_left ContinuousMap.cancel_left

instance [Nonempty Œ±] [Nontrivial Œ≤] : Nontrivial C(Œ±, Œ≤) :=
  ‚ü®let ‚ü®b‚ÇÅ, b‚ÇÇ, hb‚ü© := exists_pair_ne Œ≤
  ‚ü®const _ b‚ÇÅ, const _ b‚ÇÇ, fun h => hb <| DFunLike.congr_fun h <| Classical.arbitrary Œ±‚ü©‚ü©

section Prod

variable {Œ±‚ÇÅ Œ±‚ÇÇ Œ≤‚ÇÅ Œ≤‚ÇÇ : Type*} [TopologicalSpace Œ±‚ÇÅ] [TopologicalSpace Œ±‚ÇÇ] [TopologicalSpace Œ≤‚ÇÅ]
  [TopologicalSpace Œ≤‚ÇÇ]

/-- `Prod.fst : (x, y) ‚Ü¶ x` as a bundled continuous map. -/
@[simps (config := .asFn)]
def fst : C(Œ± √ó Œ≤, Œ±) where
  toFun := Prod.fst

/-- `Prod.snd : (x, y) ‚Ü¶ y` as a bundled continuous map. -/
@[simps (config := .asFn)]
def snd : C(Œ± √ó Œ≤, Œ≤) where
  toFun := Prod.snd

/-- Given two continuous maps `f` and `g`, this is the continuous map `x ‚Ü¶ (f x, g x)`. -/
def prodMk (f : C(Œ±, Œ≤‚ÇÅ)) (g : C(Œ±, Œ≤‚ÇÇ)) : C(Œ±, Œ≤‚ÇÅ √ó Œ≤‚ÇÇ) where
  toFun x := (f x, g x)
#align continuous_map.prod_mk ContinuousMap.prodMk

/-- Given two continuous maps `f` and `g`, this is the continuous map `(x, y) ‚Ü¶ (f x, g y)`. -/
@[simps]
def prodMap (f : C(Œ±‚ÇÅ, Œ±‚ÇÇ)) (g : C(Œ≤‚ÇÅ, Œ≤‚ÇÇ)) : C(Œ±‚ÇÅ √ó Œ≤‚ÇÅ, Œ±‚ÇÇ √ó Œ≤‚ÇÇ) where
  toFun := Prod.map f g
  continuous_toFun := f.continuous.prod_map g.continuous
  -- Porting note: proof was `continuity`
#align continuous_map.prod_map ContinuousMap.prodMap

@[simp]
theorem prod_eval (f : C(Œ±, Œ≤‚ÇÅ)) (g : C(Œ±, Œ≤‚ÇÇ)) (a : Œ±) : (prodMk f g) a = (f a, g a) :=
  rfl
#align continuous_map.prod_eval ContinuousMap.prod_eval

/-- `Prod.swap` bundled as a `ContinuousMap`. -/
@[simps!]
def prodSwap : C(Œ± √ó Œ≤, Œ≤ √ó Œ±) := .prodMk .snd .fst

end Prod

section Sigma

variable {I A : Type*} {X : I ‚Üí Type*} [TopologicalSpace A] [‚àÄ i, TopologicalSpace (X i)]

/-- `Sigma.mk i` as a bundled continuous map. -/
@[simps apply]
def sigmaMk (i : I) : C(X i, Œ£ i, X i) where
  toFun := Sigma.mk i

/--
To give a continuous map out of a disjoint union, it suffices to give a continuous map out of
each term. This is `Sigma.uncurry` for continuous maps.
-/
@[simps]
def sigma (f : ‚àÄ i, C(X i, A)) : C((Œ£ i, X i), A) where
  toFun ig := f ig.fst ig.snd

variable (A X) in
/--
Giving a continuous map out of a disjoint union is the same as giving a continuous map out of
each term. This is a version of `Equiv.piCurry`¬†for continuous maps.
-/
@[simps]
def sigmaEquiv : (‚àÄ i, C(X i, A)) ‚âÉ C((Œ£ i, X i), A) where
  toFun := sigma
  invFun f i := f.comp (sigmaMk i)
  left_inv := by intro; ext; simp
  right_inv := by intro; ext; simp

end Sigma

section Pi

variable {I A : Type*} {X Y : I ‚Üí Type*} [TopologicalSpace A] [‚àÄ i, TopologicalSpace (X i)]
  [‚àÄ i, TopologicalSpace (Y i)]

/-- Abbreviation for product of continuous maps, which is continuous -/
def pi (f : ‚àÄ i, C(A, X i)) : C(A, ‚àÄ i, X i) where
  toFun (a : A) (i : I) := f i a
#align continuous_map.pi ContinuousMap.pi

@[simp]
theorem pi_eval (f : ‚àÄ i, C(A, X i)) (a : A) : (pi f) a = fun i : I => (f i) a :=
  rfl
#align continuous_map.pi_eval ContinuousMap.pi_eval

/-- Evaluation at point as a bundled continuous map. -/
@[simps (config := .asFn)]
def eval (i : I) : C(‚àÄ j, X j, X i) where
  toFun := Function.eval i

variable (A X) in
/--
Giving a continuous map out of a disjoint union is the same as giving a continuous map out of
each term
-/
@[simps]
def piEquiv : (‚àÄ i, C(A, X i)) ‚âÉ C(A, ‚àÄ i, X i) where
  toFun := pi
  invFun f i := (eval i).comp f
  left_inv := by intro; ext; simp [pi]
  right_inv := by intro; ext; simp [pi]

/-- Combine a collection of bundled continuous maps `C(X i, Y i)` into a bundled continuous map
`C(‚àÄ i, X i, ‚àÄ i, Y i)`. -/
@[simps!]
def piMap (f : ‚àÄ i, C(X i, Y i)) : C((i : I) ‚Üí X i, (i : I) ‚Üí Y i) :=
  .pi fun i ‚Ü¶ (f i).comp (eval i)

/-- "Precomposition" as a continuous map between dependent types. -/
def precomp {Œπ : Type*} (œÜ : Œπ ‚Üí I) : C((i : I) ‚Üí X i, (i : Œπ) ‚Üí X (œÜ i)) :=
  ‚ü®_, Pi.continuous_precomp' œÜ‚ü©

end Pi

section Restrict

variable (s : Set Œ±)

/-- The restriction of a continuous function `Œ± ‚Üí Œ≤` to a subset `s` of `Œ±`. -/
def restrict (f : C(Œ±, Œ≤)) : C(s, Œ≤) where
  toFun := f ‚àò ((‚Üë) : s ‚Üí Œ±)
#align continuous_map.restrict ContinuousMap.restrict

@[simp]
theorem coe_restrict (f : C(Œ±, Œ≤)) : ‚áë(f.restrict s) = f ‚àò ((‚Üë) : s ‚Üí Œ±) :=
  rfl
#align continuous_map.coe_restrict ContinuousMap.coe_restrict

@[simp]
theorem restrict_apply (f : C(Œ±, Œ≤)) (s : Set Œ±) (x : s) : f.restrict s x = f x :=
  rfl
#align continuous_map.restrict_apply ContinuousMap.restrict_apply

@[simp]
theorem restrict_apply_mk (f : C(Œ±, Œ≤)) (s : Set Œ±) (x : Œ±) (hx : x ‚àà s) :
    f.restrict s ‚ü®x, hx‚ü© = f x :=
  rfl
#align continuous_map.restrict_apply_mk ContinuousMap.restrict_apply_mk

theorem injective_restrict [T2Space Œ≤] {s : Set Œ±} (hs : Dense s) :
    Injective (restrict s : C(Œ±, Œ≤) ‚Üí C(s, Œ≤)) := fun f g h ‚Ü¶
  DFunLike.ext' <| f.continuous.ext_on hs g.continuous <| Set.restrict_eq_restrict_iff.1 <|
    congr_arg DFunLike.coe h

/-- The restriction of a continuous map to the preimage of a set. -/
@[simps]
def restrictPreimage (f : C(Œ±, Œ≤)) (s : Set Œ≤) : C(f ‚Åª¬π' s, s) :=
  ‚ü®s.restrictPreimage f, continuous_iff_continuousAt.mpr fun _ => f.2.continuousAt.restrictPreimage‚ü©
#align continuous_map.restrict_preimage ContinuousMap.restrictPreimage

end Restrict

section Gluing

variable {Œπ : Type*} (S : Œπ ‚Üí Set Œ±) (œÜ : ‚àÄ i : Œπ, C(S i, Œ≤))
  (hœÜ : ‚àÄ (i j) (x : Œ±) (hxi : x ‚àà S i) (hxj : x ‚àà S j), œÜ i ‚ü®x, hxi‚ü© = œÜ j ‚ü®x, hxj‚ü©)
  (hS : ‚àÄ x : Œ±, ‚àÉ i, S i ‚àà ùìù x)

/-- A family `œÜ i` of continuous maps `C(S i, Œ≤)`, where the domains `S i` contain a neighbourhood
of each point in `Œ±` and the functions `œÜ i` agree pairwise on intersections, can be glued to
construct a continuous map in `C(Œ±, Œ≤)`. -/
noncomputable def liftCover : C(Œ±, Œ≤) :=
  haveI H : ‚ãÉ i, S i = Set.univ :=
    Set.iUnion_eq_univ_iff.2 fun x ‚Ü¶ (hS x).imp fun _ ‚Ü¶ mem_of_mem_nhds
  mk (Set.liftCover S (fun i ‚Ü¶ œÜ i) hœÜ H) <| continuous_of_cover_nhds hS fun i ‚Ü¶ by
    rw [continuousOn_iff_continuous_restrict]
    simpa (config := { unfoldPartialApp := true }) only [Set.restrict, Set.liftCover_coe] using
      (œÜ i).continuous
#align continuous_map.lift_cover ContinuousMap.liftCover

variable {S œÜ hœÜ hS}

@[simp]
theorem liftCover_coe {i : Œπ} (x : S i) : liftCover S œÜ hœÜ hS x = œÜ i x := by
  rw [liftCover, coe_mk, Set.liftCover_coe _]
#align continuous_map.lift_cover_coe ContinuousMap.liftCover_coe

-- @[simp] -- Porting note: the simpNF linter complained
theorem liftCover_restrict {i : Œπ} : (liftCover S œÜ hœÜ hS).restrict (S i) = œÜ i := by
  ext
  simp only [coe_restrict, Function.comp_apply, liftCover_coe]
#align continuous_map.lift_cover_restrict ContinuousMap.liftCover_restrict

variable (A : Set (Set Œ±)) (F : ‚àÄ s ‚àà A, C(s, Œ≤))
  (hF : ‚àÄ (s) (hs : s ‚àà A) (t) (ht : t ‚àà A) (x : Œ±) (hxi : x ‚àà s) (hxj : x ‚àà t),
    F s hs ‚ü®x, hxi‚ü© = F t ht ‚ü®x, hxj‚ü©)
  (hA : ‚àÄ x : Œ±, ‚àÉ i ‚àà A, i ‚àà ùìù x)

/-- A family `F s` of continuous maps `C(s, Œ≤)`, where (1) the domains `s` are taken from a set `A`
of sets in `Œ±` which contain a neighbourhood of each point in `Œ±` and (2) the functions `F s` agree
pairwise on intersections, can be glued to construct a continuous map in `C(Œ±, Œ≤)`. -/
noncomputable def liftCover' : C(Œ±, Œ≤) := by
  let S : A ‚Üí Set Œ± := (‚Üë)
  let F : ‚àÄ i : A, C(i, Œ≤) := fun i => F i i.prop
  refine liftCover S F (fun i j => hF i i.prop j j.prop) ?_
  intro x
  obtain ‚ü®s, hs, hsx‚ü© := hA x
  exact ‚ü®‚ü®s, hs‚ü©, hsx‚ü©
#align continuous_map.lift_cover' ContinuousMap.liftCover'

variable {A F hF hA}

-- Porting note: did not need `by delta liftCover'; exact` in mathlib3; goal was
-- closed by `liftCover_coe x'`
-- Might be something to do with the `let`s in the definition of `liftCover'`?
@[simp]
theorem liftCover_coe' {s : Set Œ±} {hs : s ‚àà A} (x : s) : liftCover' A F hF hA x = F s hs x :=
  let x' : ((‚Üë) : A ‚Üí Set Œ±) ‚ü®s, hs‚ü© := x
  by delta liftCover'; exact liftCover_coe x'
#align continuous_map.lift_cover_coe' ContinuousMap.liftCover_coe'

-- Porting note: porting program suggested `ext <| liftCover_coe'`
@[simp]
theorem liftCover_restrict' {s : Set Œ±} {hs : s ‚àà A} :
    (liftCover' A F hF hA).restrict s = F s hs := ext <| liftCover_coe' (hF := hF) (hA := hA)
#align continuous_map.lift_cover_restrict' ContinuousMap.liftCover_restrict'

end Gluing

end ContinuousMap

section restrictContinuous

variable {Œ± : Type*} {Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤]

open Classical in
noncomputable def Set.restrictContinuous (s : Set Œ±) (f : Œ± ‚Üí Œ≤) (b : Œ≤) : C(s, Œ≤) :=
  if h : ContinuousOn f s then ‚ü®s.restrict f, h.restrict‚ü©
  else ContinuousMap.const s b

lemma Set.restrictContinuous_of_continuous {s : Set Œ±} {f : Œ± ‚Üí Œ≤} {b : Œ≤} (hf : ContinuousOn f s) :
    s.restrictContinuous f b = ‚ü®s.restrict f, hf.restrict‚ü© := by
  simp only [Set.restrictContinuous]
  split <;> simp

lemma Set.restrictContinuous_of_not_continuous {s : Set Œ±} {f : Œ± ‚Üí Œ≤} {b : Œ≤}
    (hf : ¬¨ ContinuousOn f s) :
    s.restrictContinuous f b = (ContinuousMap.const s b) := by
  simp only [Set.restrictContinuous]
  split
  case isTrue h => exact False.elim (hf h)
  case isFalse h => simp

end restrictContinuous

section Lift

variable {X Y Z : Type*} [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]
    {f : C(X, Y)}

/-- `Setoid.quotientKerEquivOfRightInverse` as a homeomorphism. -/
@[simps!]
def Function.RightInverse.homeomorph {f' : C(Y, X)} (hf : Function.RightInverse f' f) :
    Quotient (Setoid.ker f) ‚âÉ‚Çú Y where
  toEquiv := Setoid.quotientKerEquivOfRightInverse _ _ hf
  continuous_toFun := quotientMap_quot_mk.continuous_iff.mpr f.continuous
  continuous_invFun := continuous_quotient_mk'.comp f'.continuous

namespace QuotientMap

/--
The homeomorphism from the quotient of a quotient map to its codomain. This is
`Setoid.quotientKerEquivOfSurjective` as a homeomorphism.
-/
@[simps!]
noncomputable def homeomorph (hf : QuotientMap f) : Quotient (Setoid.ker f) ‚âÉ‚Çú Y where
  toEquiv := Setoid.quotientKerEquivOfSurjective _ hf.surjective
  continuous_toFun := quotientMap_quot_mk.continuous_iff.mpr hf.continuous
  continuous_invFun := by
    rw [hf.continuous_iff]
    convert continuous_quotient_mk'
    ext
    simp only [Equiv.invFun_as_coe, Function.comp_apply,
      (Setoid.quotientKerEquivOfSurjective f hf.surjective).symm_apply_eq]
    rfl

variable (hf : QuotientMap f) (g : C(X, Z)) (h : Function.FactorsThrough g f)

/-- Descend a continuous map, which is constant on the fibres, along a quotient map. -/
@[simps]
noncomputable def lift : C(Y, Z) where
  toFun := ((fun i ‚Ü¶ Quotient.liftOn' i g (fun _ _ (hab : f _ = f _) ‚Ü¶ h hab)) :
    Quotient (Setoid.ker f) ‚Üí Z) ‚àò hf.homeomorph.symm
  continuous_toFun := Continuous.comp (continuous_quot_lift _ g.2) (Homeomorph.continuous _)

/--
The obvious triangle induced by `QuotientMap.lift`¬†commutes:
```
     g
  X --‚Üí Z
  |   ‚Üó
f |  / hf.lift g h
  v /
  Y
```
-/
@[simp]
theorem lift_comp : (hf.lift g h).comp f = g := by
  ext
  simpa using h (Function.rightInverse_surjInv _ _)

/-- `QuotientMap.lift` as an equivalence. -/
@[simps]
noncomputable def liftEquiv : { g : C(X, Z) // Function.FactorsThrough g f} ‚âÉ C(Y, Z) where
  toFun g := hf.lift g g.prop
  invFun g := ‚ü®g.comp f, fun _ _ h ‚Ü¶ by simp only [ContinuousMap.comp_apply]; rw [h]‚ü©
  left_inv := by intro; simp
  right_inv := by
    intro g
    ext a
    simpa using congrArg g (Function.rightInverse_surjInv hf.surjective a)

end QuotientMap

end Lift

namespace Homeomorph

variable {Œ± Œ≤ Œ≥ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [TopologicalSpace Œ≥]
variable (f : Œ± ‚âÉ‚Çú Œ≤) (g : Œ≤ ‚âÉ‚Çú Œ≥)

/-- The forward direction of a homeomorphism, as a bundled continuous map. -/
@[simps]
def toContinuousMap (e : Œ± ‚âÉ‚Çú Œ≤) : C(Œ±, Œ≤) :=
  ‚ü®e, e.continuous_toFun‚ü©
#align homeomorph.to_continuous_map Homeomorph.toContinuousMap
#align homeomorph.to_continuous_map_apply Homeomorph.toContinuousMap_apply

/-- `Homeomorph.toContinuousMap` as a coercion. -/
instance : Coe (Œ± ‚âÉ‚Çú Œ≤) C(Œ±, Œ≤) :=
  ‚ü®Homeomorph.toContinuousMap‚ü©

-- Porting note: Syntactic tautology
/- theorem toContinuousMap_as_coe : f.toContinuousMap = f :=
  rfl
-/
#noalign homeomorph.to_continuous_map_as_coe

@[simp]
theorem coe_refl : (Homeomorph.refl Œ± : C(Œ±, Œ±)) = ContinuousMap.id Œ± :=
  rfl
#align homeomorph.coe_refl Homeomorph.coe_refl

@[simp]
theorem coe_trans : (f.trans g : C(Œ±, Œ≥)) = (g : C(Œ≤, Œ≥)).comp f :=
  rfl
#align homeomorph.coe_trans Homeomorph.coe_trans

/-- Left inverse to a continuous map from a homeomorphism, mirroring `Equiv.symm_comp_self`. -/
@[simp]
theorem symm_comp_toContinuousMap :
    (f.symm : C(Œ≤, Œ±)).comp (f : C(Œ±, Œ≤)) = ContinuousMap.id Œ± := by
  rw [‚Üê coe_trans, self_trans_symm, coe_refl]
#align homeomorph.symm_comp_to_continuous_map Homeomorph.symm_comp_toContinuousMap

/-- Right inverse to a continuous map from a homeomorphism, mirroring `Equiv.self_comp_symm`. -/
@[simp]
theorem toContinuousMap_comp_symm :
    (f : C(Œ±, Œ≤)).comp (f.symm : C(Œ≤, Œ±)) = ContinuousMap.id Œ≤ := by
  rw [‚Üê coe_trans, symm_trans_self, coe_refl]
#align homeomorph.to_continuous_map_comp_symm Homeomorph.toContinuousMap_comp_symm

end Homeomorph
