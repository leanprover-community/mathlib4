/-
Copyright (c) 2020 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri
-/
import Mathlib.Data.Set.UnionLift
import Mathlib.Topology.Homeomorph

#align_import topology.continuous_function.basic from "leanprover-community/mathlib"@"55d771df074d0dd020139ee1cd4b95521422df9f"

/-!
# Continuous bundled maps

In this file we define the type `ContinuousMap` of continuous bundled maps.

We use the `DFunLike` design, so each type of morphisms has a companion typeclass which is meant to
be satisfied by itself and all stricter types.
-/


open Function
open scoped Topology

/-- The type of continuous maps from `Î±` to `Î²`.

When possible, instead of parametrizing results over `(f : C(Î±, Î²))`,
you should parametrize over `{F : Type*} [ContinuousMapClass F Î± Î²] (f : F)`.

When you extend this structure, make sure to extend `ContinuousMapClass`. -/
structure ContinuousMap (Î± Î² : Type*) [TopologicalSpace Î±] [TopologicalSpace Î²] where
  /-- The function `Î± â†’ Î²` -/
  protected toFun : Î± â†’ Î²
  /-- Proposition that `toFun` is continuous -/
  protected continuous_toFun : Continuous toFun := by continuity
#align continuous_map ContinuousMap

/-- The type of continuous maps from `Î±` to `Î²`. -/
notation "C(" Î± ", " Î² ")" => ContinuousMap Î± Î²

section

/-- `ContinuousMapClass F Î± Î²` states that `F` is a type of continuous maps.

You should extend this class when you extend `ContinuousMap`. -/
class ContinuousMapClass (F Î± Î² : Type*) [TopologicalSpace Î±] [TopologicalSpace Î²]
    [FunLike F Î± Î²] : Prop where
  /-- Continuity -/
  map_continuous (f : F) : Continuous f
#align continuous_map_class ContinuousMapClass

end

export ContinuousMapClass (map_continuous)

attribute [continuity, fun_prop] map_continuous

section ContinuousMapClass

variable {F Î± Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²] [FunLike F Î± Î²]
variable [ContinuousMapClass F Î± Î²]

theorem map_continuousAt (f : F) (a : Î±) : ContinuousAt f a :=
  (map_continuous f).continuousAt
#align map_continuous_at map_continuousAt

theorem map_continuousWithinAt (f : F) (s : Set Î±) (a : Î±) : ContinuousWithinAt f s a :=
  (map_continuous f).continuousWithinAt
#align map_continuous_within_at map_continuousWithinAt

/-- Coerce a bundled morphism with a `ContinuousMapClass` instance to a `ContinuousMap`. -/
@[coe] def toContinuousMap (f : F) : C(Î±, Î²) := âŸ¨f, map_continuous fâŸ©

instance : CoeTC F C(Î±, Î²) := âŸ¨toContinuousMapâŸ©

end ContinuousMapClass

/-! ### Continuous maps-/


namespace ContinuousMap

variable {Î± Î² Î³ Î´ : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²] [TopologicalSpace Î³]
  [TopologicalSpace Î´]

instance funLike : FunLike C(Î±, Î²) Î± Î² where
  coe := ContinuousMap.toFun
  coe_injective' f g h := by cases f; cases g; congr

instance toContinuousMapClass : ContinuousMapClass C(Î±, Î²) Î± Î² where
  map_continuous := ContinuousMap.continuous_toFun

@[simp]
theorem toFun_eq_coe {f : C(Î±, Î²)} : f.toFun = (f : Î± â†’ Î²) :=
  rfl
#align continuous_map.to_fun_eq_coe ContinuousMap.toFun_eq_coe

instance : CanLift (Î± â†’ Î²) C(Î±, Î²) DFunLike.coe Continuous := âŸ¨fun f hf â†¦ âŸ¨âŸ¨f, hfâŸ©, rflâŸ©âŸ©

/-- See note [custom simps projection]. -/
def Simps.apply (f : C(Î±, Î²)) : Î± â†’ Î² := f

-- this must come after the coe_to_fun definition
initialize_simps_projections ContinuousMap (toFun â†’ apply)

@[simp] -- Porting note: removed `norm_cast` attribute
protected theorem coe_coe {F : Type*} [FunLike F Î± Î²] [ContinuousMapClass F Î± Î²] (f : F) :
    â‡‘(f : C(Î±, Î²)) = f :=
  rfl
#align continuous_map.coe_coe ContinuousMap.coe_coe

@[ext]
theorem ext {f g : C(Î±, Î²)} (h : âˆ€ a, f a = g a) : f = g :=
  DFunLike.ext _ _ h
#align continuous_map.ext ContinuousMap.ext

/-- Copy of a `ContinuousMap` with a new `toFun` equal to the old one. Useful to fix definitional
equalities. -/
protected def copy (f : C(Î±, Î²)) (f' : Î± â†’ Î²) (h : f' = f) : C(Î±, Î²) where
  toFun := f'
  continuous_toFun := h.symm â–¸ f.continuous_toFun
#align continuous_map.copy ContinuousMap.copy

@[simp]
theorem coe_copy (f : C(Î±, Î²)) (f' : Î± â†’ Î²) (h : f' = f) : â‡‘(f.copy f' h) = f' :=
  rfl
#align continuous_map.coe_copy ContinuousMap.coe_copy

theorem copy_eq (f : C(Î±, Î²)) (f' : Î± â†’ Î²) (h : f' = f) : f.copy f' h = f :=
  DFunLike.ext' h
#align continuous_map.copy_eq ContinuousMap.copy_eq

variable {f g : C(Î±, Î²)}

/-- Deprecated. Use `map_continuous` instead. -/
protected theorem continuous (f : C(Î±, Î²)) : Continuous f :=
  f.continuous_toFun
#align continuous_map.continuous ContinuousMap.continuous

@[continuity]
theorem continuous_set_coe (s : Set C(Î±, Î²)) (f : s) : Continuous (f : Î± â†’ Î²) :=
  f.1.continuous
#align continuous_map.continuous_set_coe ContinuousMap.continuous_set_coe

/-- Deprecated. Use `map_continuousAt` instead. -/
protected theorem continuousAt (f : C(Î±, Î²)) (x : Î±) : ContinuousAt f x :=
  f.continuous.continuousAt
#align continuous_map.continuous_at ContinuousMap.continuousAt

/-- Deprecated. Use `DFunLike.congr_fun` instead. -/
protected theorem congr_fun {f g : C(Î±, Î²)} (H : f = g) (x : Î±) : f x = g x :=
  H â–¸ rfl
#align continuous_map.congr_fun ContinuousMap.congr_fun

/-- Deprecated. Use `DFunLike.congr_arg` instead. -/
protected theorem congr_arg (f : C(Î±, Î²)) {x y : Î±} (h : x = y) : f x = f y :=
  h â–¸ rfl
#align continuous_map.congr_arg ContinuousMap.congr_arg

theorem coe_injective : @Function.Injective C(Î±, Î²) (Î± â†’ Î²) (â†‘) := fun f g h => by
  cases f; cases g; congr
#align continuous_map.coe_injective ContinuousMap.coe_injective

@[simp]
theorem coe_mk (f : Î± â†’ Î²) (h : Continuous f) : â‡‘(âŸ¨f, hâŸ© : C(Î±, Î²)) = f :=
  rfl
#align continuous_map.coe_mk ContinuousMap.coe_mk

theorem map_specializes (f : C(Î±, Î²)) {x y : Î±} (h : x â¤³ y) : f x â¤³ f y :=
  h.map f.2
#align continuous_map.map_specializes ContinuousMap.map_specializes

section

variable (Î± Î²)

/--
The continuous functions from `Î±` to `Î²` are the same as the plain functions when `Î±` is discrete.
-/
@[simps]
def equivFnOfDiscrete [DiscreteTopology Î±] : C(Î±, Î²) â‰ƒ (Î± â†’ Î²) :=
  âŸ¨fun f => f,
    fun f => âŸ¨f, continuous_of_discreteTopologyâŸ©,
    fun _ => by ext; rfl,
    fun _ => by ext; rflâŸ©
#align continuous_map.equiv_fn_of_discrete ContinuousMap.equivFnOfDiscrete

end

variable (Î±)

/-- The identity as a continuous map. -/
protected def id : C(Î±, Î±) where
  toFun := id
#align continuous_map.id ContinuousMap.id

@[simp]
theorem coe_id : â‡‘(ContinuousMap.id Î±) = id :=
  rfl
#align continuous_map.coe_id ContinuousMap.coe_id

/-- The constant map as a continuous map. -/
def const (b : Î²) : C(Î±, Î²) where
  toFun := fun _ : Î± => b
#align continuous_map.const ContinuousMap.const

@[simp]
theorem coe_const (b : Î²) : â‡‘(const Î± b) = Function.const Î± b :=
  rfl
#align continuous_map.coe_const ContinuousMap.coe_const

/-- `Function.const Î± b` as a bundled continuous function of `b`. -/
@[simps (config := .asFn)]
def constPi : C(Î², Î± â†’ Î²) where
  toFun b := Function.const Î± b

instance [Inhabited Î²] : Inhabited C(Î±, Î²) :=
  âŸ¨const Î± defaultâŸ©

variable {Î±}

@[simp]
theorem id_apply (a : Î±) : ContinuousMap.id Î± a = a :=
  rfl
#align continuous_map.id_apply ContinuousMap.id_apply

@[simp]
theorem const_apply (b : Î²) (a : Î±) : const Î± b a = b :=
  rfl
#align continuous_map.const_apply ContinuousMap.const_apply

/-- The composition of continuous maps, as a continuous map. -/
def comp (f : C(Î², Î³)) (g : C(Î±, Î²)) : C(Î±, Î³) where
  toFun := f âˆ˜ g
#align continuous_map.comp ContinuousMap.comp

@[simp]
theorem coe_comp (f : C(Î², Î³)) (g : C(Î±, Î²)) : â‡‘(comp f g) = f âˆ˜ g :=
  rfl
#align continuous_map.coe_comp ContinuousMap.coe_comp

@[simp]
theorem comp_apply (f : C(Î², Î³)) (g : C(Î±, Î²)) (a : Î±) : comp f g a = f (g a) :=
  rfl
#align continuous_map.comp_apply ContinuousMap.comp_apply

@[simp]
theorem comp_assoc (f : C(Î³, Î´)) (g : C(Î², Î³)) (h : C(Î±, Î²)) :
    (f.comp g).comp h = f.comp (g.comp h) :=
  rfl
#align continuous_map.comp_assoc ContinuousMap.comp_assoc

@[simp]
theorem id_comp (f : C(Î±, Î²)) : (ContinuousMap.id _).comp f = f :=
  ext fun _ => rfl
#align continuous_map.id_comp ContinuousMap.id_comp

@[simp]
theorem comp_id (f : C(Î±, Î²)) : f.comp (ContinuousMap.id _) = f :=
  ext fun _ => rfl
#align continuous_map.comp_id ContinuousMap.comp_id

@[simp]
theorem const_comp (c : Î³) (f : C(Î±, Î²)) : (const Î² c).comp f = const Î± c :=
  ext fun _ => rfl
#align continuous_map.const_comp ContinuousMap.const_comp

@[simp]
theorem comp_const (f : C(Î², Î³)) (b : Î²) : f.comp (const Î± b) = const Î± (f b) :=
  ext fun _ => rfl
#align continuous_map.comp_const ContinuousMap.comp_const

@[simp]
theorem cancel_right {fâ‚ fâ‚‚ : C(Î², Î³)} {g : C(Î±, Î²)} (hg : Surjective g) :
    fâ‚.comp g = fâ‚‚.comp g â†” fâ‚ = fâ‚‚ :=
  âŸ¨fun h => ext <| hg.forall.2 <| DFunLike.ext_iff.1 h, congr_arg (ContinuousMap.comp Â· g)âŸ©
#align continuous_map.cancel_right ContinuousMap.cancel_right

@[simp]
theorem cancel_left {f : C(Î², Î³)} {gâ‚ gâ‚‚ : C(Î±, Î²)} (hf : Injective f) :
    f.comp gâ‚ = f.comp gâ‚‚ â†” gâ‚ = gâ‚‚ :=
  âŸ¨fun h => ext fun a => hf <| by rw [â† comp_apply, h, comp_apply], congr_arg _âŸ©
#align continuous_map.cancel_left ContinuousMap.cancel_left

instance [Nonempty Î±] [Nontrivial Î²] : Nontrivial C(Î±, Î²) :=
  âŸ¨let âŸ¨bâ‚, bâ‚‚, hbâŸ© := exists_pair_ne Î²
  âŸ¨const _ bâ‚, const _ bâ‚‚, fun h => hb <| DFunLike.congr_fun h <| Classical.arbitrary Î±âŸ©âŸ©

section Prod

variable {Î±â‚ Î±â‚‚ Î²â‚ Î²â‚‚ : Type*} [TopologicalSpace Î±â‚] [TopologicalSpace Î±â‚‚] [TopologicalSpace Î²â‚]
  [TopologicalSpace Î²â‚‚]

/-- `Prod.fst : (x, y) â†¦ x` as a bundled continuous map. -/
@[simps (config := .asFn)]
def fst : C(Î± Ã— Î², Î±) where
  toFun := Prod.fst

/-- `Prod.snd : (x, y) â†¦ y` as a bundled continuous map. -/
@[simps (config := .asFn)]
def snd : C(Î± Ã— Î², Î²) where
  toFun := Prod.snd

/-- Given two continuous maps `f` and `g`, this is the continuous map `x â†¦ (f x, g x)`. -/
def prodMk (f : C(Î±, Î²â‚)) (g : C(Î±, Î²â‚‚)) : C(Î±, Î²â‚ Ã— Î²â‚‚) where
  toFun x := (f x, g x)
#align continuous_map.prod_mk ContinuousMap.prodMk

/-- Given two continuous maps `f` and `g`, this is the continuous map `(x, y) â†¦ (f x, g y)`. -/
@[simps]
def prodMap (f : C(Î±â‚, Î±â‚‚)) (g : C(Î²â‚, Î²â‚‚)) : C(Î±â‚ Ã— Î²â‚, Î±â‚‚ Ã— Î²â‚‚) where
  toFun := Prod.map f g
  continuous_toFun := f.continuous.prod_map g.continuous
  -- Porting note: proof was `continuity`
#align continuous_map.prod_map ContinuousMap.prodMap

@[simp]
theorem prod_eval (f : C(Î±, Î²â‚)) (g : C(Î±, Î²â‚‚)) (a : Î±) : (prodMk f g) a = (f a, g a) :=
  rfl
#align continuous_map.prod_eval ContinuousMap.prod_eval

/-- `Prod.swap` bundled as a `ContinuousMap`. -/
@[simps!]
def prodSwap : C(Î± Ã— Î², Î² Ã— Î±) := .prodMk .snd .fst

end Prod

section Sigma

variable {I A : Type*} {X : I â†’ Type*} [TopologicalSpace A] [âˆ€ i, TopologicalSpace (X i)]

/-- `Sigma.mk i` as a bundled continuous map. -/
@[simps apply]
def sigmaMk (i : I) : C(X i, Î£ i, X i) where
  toFun := Sigma.mk i

/--
To give a continuous map out of a disjoint union, it suffices to give a continuous map out of
each term. This is `Sigma.uncurry` for continuous maps.
-/
@[simps]
def sigma (f : âˆ€ i, C(X i, A)) : C((Î£ i, X i), A) where
  toFun ig := f ig.fst ig.snd

variable (A X) in
/--
Giving a continuous map out of a disjoint union is the same as giving a continuous map out of
each term. This is a version of `Equiv.piCurry`Â for continuous maps.
-/
@[simps]
def sigmaEquiv : (âˆ€ i, C(X i, A)) â‰ƒ C((Î£ i, X i), A) where
  toFun := sigma
  invFun f i := f.comp (sigmaMk i)
  left_inv := by intro; ext; simp
  right_inv := by intro; ext; simp

end Sigma

section Pi

variable {I A : Type*} {X Y : I â†’ Type*} [TopologicalSpace A] [âˆ€ i, TopologicalSpace (X i)]
  [âˆ€ i, TopologicalSpace (Y i)]

/-- Abbreviation for product of continuous maps, which is continuous -/
def pi (f : âˆ€ i, C(A, X i)) : C(A, âˆ€ i, X i) where
  toFun (a : A) (i : I) := f i a
#align continuous_map.pi ContinuousMap.pi

@[simp]
theorem pi_eval (f : âˆ€ i, C(A, X i)) (a : A) : (pi f) a = fun i : I => (f i) a :=
  rfl
#align continuous_map.pi_eval ContinuousMap.pi_eval

/-- Evaluation at point as a bundled continuous map. -/
@[simps (config := .asFn)]
def eval (i : I) : C(âˆ€ j, X j, X i) where
  toFun := Function.eval i

variable (A X) in
/--
Giving a continuous map out of a disjoint union is the same as giving a continuous map out of
each term
-/
@[simps]
def piEquiv : (âˆ€ i, C(A, X i)) â‰ƒ C(A, âˆ€ i, X i) where
  toFun := pi
  invFun f i := (eval i).comp f
  left_inv := by intro; ext; simp [pi]
  right_inv := by intro; ext; simp [pi]

/-- Combine a collection of bundled continuous maps `C(X i, Y i)` into a bundled continuous map
`C(âˆ€ i, X i, âˆ€ i, Y i)`. -/
@[simps!]
def piMap (f : âˆ€ i, C(X i, Y i)) : C((i : I) â†’ X i, (i : I) â†’ Y i) :=
  .pi fun i â†¦ (f i).comp (eval i)

/-- "Precomposition" as a continuous map between dependent types. -/
def precomp {Î¹ : Type*} (Ï† : Î¹ â†’ I) : C((i : I) â†’ X i, (i : Î¹) â†’ X (Ï† i)) :=
  âŸ¨_, Pi.continuous_precomp' Ï†âŸ©

end Pi

section Restrict

variable (s : Set Î±)

/-- The restriction of a continuous function `Î± â†’ Î²` to a subset `s` of `Î±`. -/
def restrict (f : C(Î±, Î²)) : C(s, Î²) where
  toFun := f âˆ˜ ((â†‘) : s â†’ Î±)
#align continuous_map.restrict ContinuousMap.restrict

@[simp]
theorem coe_restrict (f : C(Î±, Î²)) : â‡‘(f.restrict s) = f âˆ˜ ((â†‘) : s â†’ Î±) :=
  rfl
#align continuous_map.coe_restrict ContinuousMap.coe_restrict

@[simp]
theorem restrict_apply (f : C(Î±, Î²)) (s : Set Î±) (x : s) : f.restrict s x = f x :=
  rfl
#align continuous_map.restrict_apply ContinuousMap.restrict_apply

@[simp]
theorem restrict_apply_mk (f : C(Î±, Î²)) (s : Set Î±) (x : Î±) (hx : x âˆˆ s) :
    f.restrict s âŸ¨x, hxâŸ© = f x :=
  rfl
#align continuous_map.restrict_apply_mk ContinuousMap.restrict_apply_mk

theorem injective_restrict [T2Space Î²] {s : Set Î±} (hs : Dense s) :
    Injective (restrict s : C(Î±, Î²) â†’ C(s, Î²)) := fun f g h â†¦
  DFunLike.ext' <| f.continuous.ext_on hs g.continuous <| Set.restrict_eq_restrict_iff.1 <|
    congr_arg DFunLike.coe h

/-- The restriction of a continuous map to the preimage of a set. -/
@[simps]
def restrictPreimage (f : C(Î±, Î²)) (s : Set Î²) : C(f â»Â¹' s, s) :=
  âŸ¨s.restrictPreimage f, continuous_iff_continuousAt.mpr fun _ => f.2.continuousAt.restrictPreimageâŸ©
#align continuous_map.restrict_preimage ContinuousMap.restrictPreimage

end Restrict

section Gluing

variable {Î¹ : Type*} (S : Î¹ â†’ Set Î±) (Ï† : âˆ€ i : Î¹, C(S i, Î²))
  (hÏ† : âˆ€ (i j) (x : Î±) (hxi : x âˆˆ S i) (hxj : x âˆˆ S j), Ï† i âŸ¨x, hxiâŸ© = Ï† j âŸ¨x, hxjâŸ©)
  (hS : âˆ€ x : Î±, âˆƒ i, S i âˆˆ ğ“ x)

/-- A family `Ï† i` of continuous maps `C(S i, Î²)`, where the domains `S i` contain a neighbourhood
of each point in `Î±` and the functions `Ï† i` agree pairwise on intersections, can be glued to
construct a continuous map in `C(Î±, Î²)`. -/
noncomputable def liftCover : C(Î±, Î²) :=
  haveI H : â‹ƒ i, S i = Set.univ :=
    Set.iUnion_eq_univ_iff.2 fun x â†¦ (hS x).imp fun _ â†¦ mem_of_mem_nhds
  mk (Set.liftCover S (fun i â†¦ Ï† i) hÏ† H) <| continuous_of_cover_nhds hS fun i â†¦ by
    rw [continuousOn_iff_continuous_restrict]
    simpa (config := { unfoldPartialApp := true }) only [Set.restrict, Set.liftCover_coe] using
      (Ï† i).continuous
#align continuous_map.lift_cover ContinuousMap.liftCover

variable {S Ï† hÏ† hS}

@[simp]
theorem liftCover_coe {i : Î¹} (x : S i) : liftCover S Ï† hÏ† hS x = Ï† i x := by
  rw [liftCover, coe_mk, Set.liftCover_coe _]
#align continuous_map.lift_cover_coe ContinuousMap.liftCover_coe

-- @[simp] -- Porting note: the simpNF linter complained
theorem liftCover_restrict {i : Î¹} : (liftCover S Ï† hÏ† hS).restrict (S i) = Ï† i := by
  ext
  simp only [coe_restrict, Function.comp_apply, liftCover_coe]
#align continuous_map.lift_cover_restrict ContinuousMap.liftCover_restrict

variable (A : Set (Set Î±)) (F : âˆ€ s âˆˆ A, C(s, Î²))
  (hF : âˆ€ (s) (hs : s âˆˆ A) (t) (ht : t âˆˆ A) (x : Î±) (hxi : x âˆˆ s) (hxj : x âˆˆ t),
    F s hs âŸ¨x, hxiâŸ© = F t ht âŸ¨x, hxjâŸ©)
  (hA : âˆ€ x : Î±, âˆƒ i âˆˆ A, i âˆˆ ğ“ x)

/-- A family `F s` of continuous maps `C(s, Î²)`, where (1) the domains `s` are taken from a set `A`
of sets in `Î±` which contain a neighbourhood of each point in `Î±` and (2) the functions `F s` agree
pairwise on intersections, can be glued to construct a continuous map in `C(Î±, Î²)`. -/
noncomputable def liftCover' : C(Î±, Î²) := by
  let S : A â†’ Set Î± := (â†‘)
  let F : âˆ€ i : A, C(i, Î²) := fun i => F i i.prop
  refine liftCover S F (fun i j => hF i i.prop j j.prop) ?_
  intro x
  obtain âŸ¨s, hs, hsxâŸ© := hA x
  exact âŸ¨âŸ¨s, hsâŸ©, hsxâŸ©
#align continuous_map.lift_cover' ContinuousMap.liftCover'

variable {A F hF hA}

-- Porting note: did not need `by delta liftCover'; exact` in mathlib3; goal was
-- closed by `liftCover_coe x'`
-- Might be something to do with the `let`s in the definition of `liftCover'`?
@[simp]
theorem liftCover_coe' {s : Set Î±} {hs : s âˆˆ A} (x : s) : liftCover' A F hF hA x = F s hs x :=
  let x' : ((â†‘) : A â†’ Set Î±) âŸ¨s, hsâŸ© := x
  by delta liftCover'; exact liftCover_coe x'
#align continuous_map.lift_cover_coe' ContinuousMap.liftCover_coe'

-- Porting note: porting program suggested `ext <| liftCover_coe'`
@[simp]
theorem liftCover_restrict' {s : Set Î±} {hs : s âˆˆ A} :
    (liftCover' A F hF hA).restrict s = F s hs := ext <| liftCover_coe' (hF := hF) (hA := hA)
#align continuous_map.lift_cover_restrict' ContinuousMap.liftCover_restrict'

end Gluing

end ContinuousMap

section Lift

variable {X Y Z : Type*} [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]
    {f : C(X, Y)}

/-- `Setoid.quotientKerEquivOfRightInverse` as a homeomorphism. -/
@[simps!]
def Function.RightInverse.homeomorph {f' : C(Y, X)} (hf : Function.RightInverse f' f) :
    Quotient (Setoid.ker f) â‰ƒâ‚œ Y where
  toEquiv := Setoid.quotientKerEquivOfRightInverse _ _ hf
  continuous_toFun := quotientMap_quot_mk.continuous_iff.mpr f.continuous
  continuous_invFun := continuous_quotient_mk'.comp f'.continuous

namespace QuotientMap

/--
The homeomorphism from the quotient of a quotient map to its codomain. This is
`Setoid.quotientKerEquivOfSurjective` as a homeomorphism.
-/
@[simps!]
noncomputable def homeomorph (hf : QuotientMap f) : Quotient (Setoid.ker f) â‰ƒâ‚œ Y where
  toEquiv := Setoid.quotientKerEquivOfSurjective _ hf.surjective
  continuous_toFun := quotientMap_quot_mk.continuous_iff.mpr hf.continuous
  continuous_invFun := by
    rw [hf.continuous_iff]
    convert continuous_quotient_mk'
    ext
    simp only [Equiv.invFun_as_coe, Function.comp_apply,
      (Setoid.quotientKerEquivOfSurjective f hf.surjective).symm_apply_eq]
    rfl

variable (hf : QuotientMap f) (g : C(X, Z)) (h : Function.FactorsThrough g f)

/-- Descend a continuous map, which is constant on the fibres, along a quotient map. -/
@[simps]
noncomputable def lift : C(Y, Z) where
  toFun := ((fun i â†¦ Quotient.liftOn' i g (fun _ _ (hab : f _ = f _) â†¦ h hab)) :
    Quotient (Setoid.ker f) â†’ Z) âˆ˜ hf.homeomorph.symm
  continuous_toFun := Continuous.comp (continuous_quot_lift _ g.2) (Homeomorph.continuous _)

/--
The obvious triangle induced by `QuotientMap.lift`Â commutes:
```
     g
  X --â†’ Z
  |   â†—
f |  / hf.lift g h
  v /
  Y
```
-/
@[simp]
theorem lift_comp : (hf.lift g h).comp f = g := by
  ext
  simpa using h (Function.rightInverse_surjInv _ _)

/-- `QuotientMap.lift` as an equivalence. -/
@[simps]
noncomputable def liftEquiv : { g : C(X, Z) // Function.FactorsThrough g f} â‰ƒ C(Y, Z) where
  toFun g := hf.lift g g.prop
  invFun g := âŸ¨g.comp f, fun _ _ h â†¦ by simp only [ContinuousMap.comp_apply]; rw [h]âŸ©
  left_inv := by intro; simp
  right_inv := by
    intro g
    ext a
    simpa using congrArg g (Function.rightInverse_surjInv hf.surjective a)

end QuotientMap

end Lift

namespace Homeomorph

variable {Î± Î² Î³ : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²] [TopologicalSpace Î³]
variable (f : Î± â‰ƒâ‚œ Î²) (g : Î² â‰ƒâ‚œ Î³)

/-- The forward direction of a homeomorphism, as a bundled continuous map. -/
@[simps]
def toContinuousMap (e : Î± â‰ƒâ‚œ Î²) : C(Î±, Î²) :=
  âŸ¨e, e.continuous_toFunâŸ©
#align homeomorph.to_continuous_map Homeomorph.toContinuousMap
#align homeomorph.to_continuous_map_apply Homeomorph.toContinuousMap_apply

/-- `Homeomorph.toContinuousMap` as a coercion. -/
instance : Coe (Î± â‰ƒâ‚œ Î²) C(Î±, Î²) :=
  âŸ¨Homeomorph.toContinuousMapâŸ©

-- Porting note: Syntactic tautology
/- theorem toContinuousMap_as_coe : f.toContinuousMap = f :=
  rfl
-/
#noalign homeomorph.to_continuous_map_as_coe

@[simp]
theorem coe_refl : (Homeomorph.refl Î± : C(Î±, Î±)) = ContinuousMap.id Î± :=
  rfl
#align homeomorph.coe_refl Homeomorph.coe_refl

@[simp]
theorem coe_trans : (f.trans g : C(Î±, Î³)) = (g : C(Î², Î³)).comp f :=
  rfl
#align homeomorph.coe_trans Homeomorph.coe_trans

/-- Left inverse to a continuous map from a homeomorphism, mirroring `Equiv.symm_comp_self`. -/
@[simp]
theorem symm_comp_toContinuousMap :
    (f.symm : C(Î², Î±)).comp (f : C(Î±, Î²)) = ContinuousMap.id Î± := by
  rw [â† coe_trans, self_trans_symm, coe_refl]
#align homeomorph.symm_comp_to_continuous_map Homeomorph.symm_comp_toContinuousMap

/-- Right inverse to a continuous map from a homeomorphism, mirroring `Equiv.self_comp_symm`. -/
@[simp]
theorem toContinuousMap_comp_symm :
    (f : C(Î±, Î²)).comp (f.symm : C(Î², Î±)) = ContinuousMap.id Î² := by
  rw [â† coe_trans, symm_trans_self, coe_refl]
#align homeomorph.to_continuous_map_comp_symm Homeomorph.toContinuousMap_comp_symm

end Homeomorph
