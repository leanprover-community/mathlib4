/-
Copyright (c) 2022 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Analysis.NormedSpace.Units
import Mathlib.Algebra.Algebra.Spectrum
import Mathlib.Topology.ContinuousFunction.Algebra

#align_import topology.continuous_function.units from "leanprover-community/mathlib"@"a148d797a1094ab554ad4183a4ad6f130358ef64"

/-!
# Units of continuous functions

This file concerns itself with `C(X, M)Ë£` and `C(X, MË£)` when `X` is a topological space
and `M` has some monoid structure compatible with its topology.
-/


variable {X M R ğ•œ : Type*} [TopologicalSpace X]

namespace ContinuousMap

section Monoid

variable [Monoid M] [TopologicalSpace M] [ContinuousMul M]

/-- Equivalence between continuous maps into the units of a monoid with continuous multiplication
and the units of the monoid of continuous maps. -/
-- Porting note: `simps` made bad `simp` lemmas (LHS simplifies) so we add them manually below
@[to_additive (attr := simps apply_val_apply symm_apply_apply_val)
"Equivalence between continuous maps into the additive units of an additive monoid with continuous
addition and the additive units of the additive monoid of continuous maps."]
def unitsLift : C(X, MË£) â‰ƒ C(X, M)Ë£ where
  toFun f :=
    { val := âŸ¨fun x => f x, Units.continuous_val.comp f.continuousâŸ©
      inv := âŸ¨fun x => â†‘(f x)â»Â¹, Units.continuous_val.comp (continuous_inv.comp f.continuous)âŸ©
      val_inv := ext fun x => Units.mul_inv _
      inv_val := ext fun x => Units.inv_mul _ }
  invFun f :=
    { toFun := fun x =>
        âŸ¨(f : C(X, M)) x, (â†‘fâ»Â¹ : C(X, M)) x,
          ContinuousMap.congr_fun f.mul_inv x, ContinuousMap.congr_fun f.inv_mul xâŸ©
      continuous_toFun := continuous_induced_rng.2 <|
        (f : C(X, M)).continuous.prod_mk <|
        MulOpposite.continuous_op.comp (â†‘fâ»Â¹ : C(X, M)).continuous }
  left_inv f := by ext; rfl
  right_inv f := by ext; rfl
#align continuous_map.units_lift ContinuousMap.unitsLift
#align continuous_map.add_units_lift ContinuousMap.addUnitsLift

-- Porting note: add manually because `simps` used `inv` and `simpNF` complained
@[to_additive (attr := simp)]
lemma unitsLift_apply_inv_apply (f : C(X, MË£)) (x : X) :
    (â†‘(ContinuousMap.unitsLift f)â»Â¹ : C(X, M)) x = (f x)â»Â¹ :=
  rfl

-- Porting note: add manually because `simps` used `inv` and `simpNF` complained
@[to_additive (attr := simp)]
lemma unitsLift_symm_apply_apply_inv' (f : C(X, M)Ë£) (x : X) :
    (ContinuousMap.unitsLift.symm f x)â»Â¹ = (â†‘fâ»Â¹ : C(X, M)) x := by
  rfl

end Monoid

section NormedRing

variable [NormedRing R] [CompleteSpace R]

theorem continuous_isUnit_unit {f : C(X, R)} (h : âˆ€ x, IsUnit (f x)) :
    Continuous fun x => (h x).unit := by
  refine
    continuous_induced_rng.2
      (Continuous.prod_mk f.continuous
        (MulOpposite.continuous_op.comp (continuous_iff_continuousAt.mpr fun x => ?_)))
  have := NormedRing.inverse_continuousAt (h x).unit
  simp only
  simp only [â† Ring.inverse_unit, IsUnit.unit_spec] at this âŠ¢
  exact this.comp (f.continuousAt x)
#align normed_ring.is_unit_unit_continuous ContinuousMap.continuous_isUnit_unit
-- Porting note: this had the worst namespace: `NormedRing`

/-- Construct a continuous map into the group of units of a normed ring from a function into the
normed ring and a proof that every element of the range is a unit. -/
@[simps]
noncomputable def unitsOfForallIsUnit {f : C(X, R)} (h : âˆ€ x, IsUnit (f x)) : C(X, RË£) where
  toFun x := (h x).unit
  continuous_toFun := continuous_isUnit_unit h
#align continuous_map.units_of_forall_is_unit ContinuousMap.unitsOfForallIsUnit

instance canLift :
    CanLift C(X, R) C(X, RË£) (fun f => âŸ¨fun x => f x, Units.continuous_val.comp f.continuousâŸ©)
      fun f => âˆ€ x, IsUnit (f x) where
  prf f h := âŸ¨unitsOfForallIsUnit h, by ext; rflâŸ©
#align continuous_map.can_lift ContinuousMap.canLift

theorem isUnit_iff_forall_isUnit (f : C(X, R)) : IsUnit f â†” âˆ€ x, IsUnit (f x) :=
  Iff.intro (fun h => fun x => âŸ¨unitsLift.symm h.unit x, rflâŸ©) fun h =>
    âŸ¨ContinuousMap.unitsLift (unitsOfForallIsUnit h), by ext; rflâŸ©
#align continuous_map.is_unit_iff_forall_is_unit ContinuousMap.isUnit_iff_forall_isUnit

end NormedRing

section NormedField

variable [NormedField ğ•œ] [NormedDivisionRing R] [Algebra ğ•œ R] [CompleteSpace R]

theorem isUnit_iff_forall_ne_zero (f : C(X, R)) : IsUnit f â†” âˆ€ x, f x â‰  0 := by
  simp_rw [f.isUnit_iff_forall_isUnit, isUnit_iff_ne_zero]
#align continuous_map.is_unit_iff_forall_ne_zero ContinuousMap.isUnit_iff_forall_ne_zero

theorem spectrum_eq_preimage_range (f : C(X, R)) :
    spectrum ğ•œ f = algebraMap _ _ â»Â¹' Set.range f := by
  ext x
  simp only [spectrum.mem_iff, isUnit_iff_forall_ne_zero, not_forall, sub_apply,
    algebraMap_apply, mul_one, Classical.not_not, Set.mem_range,
    sub_eq_zero, @eq_comm _ (x â€¢ 1 : R) _, Set.mem_preimage, Algebra.algebraMap_eq_smul_one,
    smul_apply, one_apply]

theorem spectrum_eq_range [CompleteSpace ğ•œ] (f : C(X, ğ•œ)) : spectrum ğ•œ f = Set.range f := by
  rw [spectrum_eq_preimage_range, Algebra.id.map_eq_id]
  exact Set.preimage_id
#align continuous_map.spectrum_eq_range ContinuousMap.spectrum_eq_range

end NormedField

end ContinuousMap
