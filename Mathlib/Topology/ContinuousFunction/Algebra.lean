/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, NicolÃ² Cavalleri
-/
import Mathlib.Algebra.Algebra.Pi
import Mathlib.Algebra.Order.Group.Lattice
import Mathlib.Algebra.Periodic
import Mathlib.Algebra.Algebra.Subalgebra.Basic
import Mathlib.Algebra.Star.StarAlgHom
import Mathlib.Tactic.FieldSimp
import Mathlib.Topology.Algebra.Module.Basic
import Mathlib.Topology.Algebra.InfiniteSum.Basic
import Mathlib.Topology.Algebra.Ring.Basic
import Mathlib.Topology.Algebra.Star
import Mathlib.Topology.Algebra.UniformGroup
import Mathlib.Topology.ContinuousFunction.Ordered
import Mathlib.Topology.UniformSpace.CompactConvergence

#align_import topology.continuous_function.algebra from "leanprover-community/mathlib"@"16e59248c0ebafabd5d071b1cd41743eb8698ffb"

/-!
# Algebraic structures over continuous functions

In this file we define instances of algebraic structures over the type `ContinuousMap Î± Î²`
(denoted `C(Î±, Î²)`) of **bundled** continuous maps from `Î±` to `Î²`. For example, `C(Î±, Î²)`
is a group when `Î²` is a group, a ring when `Î²` is a ring, etc.

For each type of algebraic structure, we also define an appropriate subobject of `Î± â†’ Î²`
with carrier `{ f : Î± â†’ Î² | Continuous f }`. For example, when `Î²` is a group, a subgroup
`continuousSubgroup Î± Î²` of `Î± â†’ Î²` is constructed with carrier `{ f : Î± â†’ Î² | Continuous f }`.

Note that, rather than using the derived algebraic structures on these subobjects
(for example, when `Î²` is a group, the derived group structure on `continuousSubgroup Î± Î²`),
one should use `C(Î±, Î²)` with the appropriate instance of the structure.
-/


--attribute [elab_without_expected_type] Continuous.comp

namespace ContinuousFunctions

variable {Î± : Type*} {Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²]
variable {f g : { f : Î± â†’ Î² | Continuous f }}

instance : CoeFun { f : Î± â†’ Î² | Continuous f } fun _ => Î± â†’ Î² :=
  âŸ¨Subtype.valâŸ©

end ContinuousFunctions

namespace ContinuousMap

variable {Î± : Type*} {Î² : Type*} {Î³ : Type*}
variable [TopologicalSpace Î±] [TopologicalSpace Î²] [TopologicalSpace Î³]

/-! ### `mul` and `add` -/

@[to_additive]
instance instMul [Mul Î²] [ContinuousMul Î²] : Mul C(Î±, Î²) :=
  âŸ¨fun f g => âŸ¨f * g, continuous_mul.comp (f.continuous.prod_mk g.continuous : _)âŸ©âŸ©
#align continuous_map.has_mul ContinuousMap.instMul
#align continuous_map.has_add ContinuousMap.instAdd

@[to_additive (attr := norm_cast, simp)]
theorem coe_mul [Mul Î²] [ContinuousMul Î²] (f g : C(Î±, Î²)) : â‡‘(f * g) = f * g :=
  rfl
#align continuous_map.coe_mul ContinuousMap.coe_mul
#align continuous_map.coe_add ContinuousMap.coe_add

@[to_additive (attr := simp)]
theorem mul_apply [Mul Î²] [ContinuousMul Î²] (f g : C(Î±, Î²)) (x : Î±) : (f * g) x = f x * g x :=
  rfl
#align continuous_map.mul_apply ContinuousMap.mul_apply
#align continuous_map.add_apply ContinuousMap.add_apply

@[to_additive (attr := simp)]
theorem mul_comp [Mul Î³] [ContinuousMul Î³] (fâ‚ fâ‚‚ : C(Î², Î³)) (g : C(Î±, Î²)) :
    (fâ‚ * fâ‚‚).comp g = fâ‚.comp g * fâ‚‚.comp g :=
  rfl
#align continuous_map.mul_comp ContinuousMap.mul_comp
#align continuous_map.add_comp ContinuousMap.add_comp

/-! ### `one` -/

@[to_additive]
instance [One Î²] : One C(Î±, Î²) :=
  âŸ¨const Î± 1âŸ©

@[to_additive (attr := norm_cast, simp)]
theorem coe_one [One Î²] : â‡‘(1 : C(Î±, Î²)) = 1 :=
  rfl
#align continuous_map.coe_one ContinuousMap.coe_one
#align continuous_map.coe_zero ContinuousMap.coe_zero

@[to_additive (attr := simp)]
theorem one_apply [One Î²] (x : Î±) : (1 : C(Î±, Î²)) x = 1 :=
  rfl
#align continuous_map.one_apply ContinuousMap.one_apply
#align continuous_map.zero_apply ContinuousMap.zero_apply

@[to_additive (attr := simp)]
theorem one_comp [One Î³] (g : C(Î±, Î²)) : (1 : C(Î², Î³)).comp g = 1 :=
  rfl
#align continuous_map.one_comp ContinuousMap.one_comp
#align continuous_map.zero_comp ContinuousMap.zero_comp

/-! ### `Nat.cast` -/

instance [NatCast Î²] : NatCast C(Î±, Î²) :=
  âŸ¨fun n => ContinuousMap.const _ nâŸ©

@[simp, norm_cast]
theorem coe_natCast [NatCast Î²] (n : â„•) : ((n : C(Î±, Î²)) : Î± â†’ Î²) = n :=
  rfl
#align continuous_map.coe_nat_cast ContinuousMap.coe_natCast

@[deprecated (since := "2024-04-17")]
alias coe_nat_cast := coe_natCast

@[simp]
theorem natCast_apply [NatCast Î²] (n : â„•) (x : Î±) : (n : C(Î±, Î²)) x = n :=
  rfl
#align continuous_map.nat_cast_apply ContinuousMap.natCast_apply

@[deprecated (since := "2024-04-17")]
alias nat_cast_apply := natCast_apply

/-! ### `Int.cast` -/

instance [IntCast Î²] : IntCast C(Î±, Î²) :=
  âŸ¨fun n => ContinuousMap.const _ nâŸ©

@[simp, norm_cast]
theorem coe_intCast [IntCast Î²] (n : â„¤) : ((n : C(Î±, Î²)) : Î± â†’ Î²) = n :=
  rfl
#align continuous_map.coe_int_cast ContinuousMap.coe_intCast

@[deprecated (since := "2024-04-17")]
alias coe_int_cast := coe_intCast

@[simp]
theorem intCast_apply [IntCast Î²] (n : â„¤) (x : Î±) : (n : C(Î±, Î²)) x = n :=
  rfl
#align continuous_map.int_cast_apply ContinuousMap.intCast_apply

@[deprecated (since := "2024-04-17")]
alias int_cast_apply := intCast_apply

/-! ### `nsmul` and `pow` -/

instance instNSMul [AddMonoid Î²] [ContinuousAdd Î²] : SMul â„• C(Î±, Î²) :=
  âŸ¨fun n f => âŸ¨n â€¢ â‡‘f, f.continuous.nsmul nâŸ©âŸ©
#align continuous_map.has_nsmul ContinuousMap.instNSMul

@[to_additive existing]
instance instPow [Monoid Î²] [ContinuousMul Î²] : Pow C(Î±, Î²) â„• :=
  âŸ¨fun f n => âŸ¨(â‡‘f) ^ n, f.continuous.pow nâŸ©âŸ©
#align continuous_map.has_pow ContinuousMap.instPow

@[to_additive (attr := norm_cast) (reorder := 7 8)]
theorem coe_pow [Monoid Î²] [ContinuousMul Î²] (f : C(Î±, Î²)) (n : â„•) : â‡‘(f ^ n) = (â‡‘f) ^ n :=
  rfl
#align continuous_map.coe_pow ContinuousMap.coe_pow
#align continuous_map.coe_nsmul ContinuousMap.coe_nsmul

@[to_additive (attr := norm_cast)]
theorem pow_apply [Monoid Î²] [ContinuousMul Î²] (f : C(Î±, Î²)) (n : â„•) (x : Î±) :
    (f ^ n) x = f x ^ n :=
  rfl
#align continuous_map.pow_apply ContinuousMap.pow_apply
#align continuous_map.nsmul_apply ContinuousMap.nsmul_apply

-- don't make auto-generated `coe_nsmul` and `nsmul_apply` simp, as the linter complains they're
-- redundant WRT `coe_smul`
attribute [simp] coe_pow pow_apply

@[to_additive]
theorem pow_comp [Monoid Î³] [ContinuousMul Î³] (f : C(Î², Î³)) (n : â„•) (g : C(Î±, Î²)) :
    (f ^ n).comp g = f.comp g ^ n :=
  rfl
#align continuous_map.pow_comp ContinuousMap.pow_comp
#align continuous_map.nsmul_comp ContinuousMap.nsmul_comp

-- don't make `nsmul_comp` simp as the linter complains it's redundant WRT `smul_comp`
attribute [simp] pow_comp

/-! ### `inv` and `neg` -/

@[to_additive]
instance [Inv Î²] [ContinuousInv Î²] : Inv C(Î±, Î²) where inv f := âŸ¨fâ»Â¹, f.continuous.invâŸ©

@[to_additive (attr := simp)]
theorem coe_inv [Inv Î²] [ContinuousInv Î²] (f : C(Î±, Î²)) : â‡‘fâ»Â¹ = (â‡‘f)â»Â¹ :=
  rfl
#align continuous_map.coe_inv ContinuousMap.coe_inv
#align continuous_map.coe_neg ContinuousMap.coe_neg

@[to_additive (attr := simp)]
theorem inv_apply [Inv Î²] [ContinuousInv Î²] (f : C(Î±, Î²)) (x : Î±) : fâ»Â¹ x = (f x)â»Â¹ :=
  rfl
#align continuous_map.inv_apply ContinuousMap.inv_apply
#align continuous_map.neg_apply ContinuousMap.neg_apply

@[to_additive (attr := simp)]
theorem inv_comp [Inv Î³] [ContinuousInv Î³] (f : C(Î², Î³)) (g : C(Î±, Î²)) :
    fâ»Â¹.comp g = (f.comp g)â»Â¹ :=
  rfl
#align continuous_map.inv_comp ContinuousMap.inv_comp
#align continuous_map.neg_comp ContinuousMap.neg_comp

/-! ### `div` and `sub` -/

@[to_additive]
instance [Div Î²] [ContinuousDiv Î²] : Div C(Î±, Î²) where
  div f g := âŸ¨f / g, f.continuous.div' g.continuousâŸ©

@[to_additive (attr := norm_cast, simp)]
theorem coe_div [Div Î²] [ContinuousDiv Î²] (f g : C(Î±, Î²)) : â‡‘(f / g) = f / g :=
  rfl
#align continuous_map.coe_div ContinuousMap.coe_div
#align continuous_map.coe_sub ContinuousMap.coe_sub

@[to_additive (attr := simp)]
theorem div_apply [Div Î²] [ContinuousDiv Î²] (f g : C(Î±, Î²)) (x : Î±) : (f / g) x = f x / g x :=
  rfl
#align continuous_map.div_apply ContinuousMap.div_apply
#align continuous_map.sub_apply ContinuousMap.sub_apply

@[to_additive (attr := simp)]
theorem div_comp [Div Î³] [ContinuousDiv Î³] (f g : C(Î², Î³)) (h : C(Î±, Î²)) :
    (f / g).comp h = f.comp h / g.comp h :=
  rfl
#align continuous_map.div_comp ContinuousMap.div_comp
#align continuous_map.sub_comp ContinuousMap.sub_comp

/-! ### `zpow` and `zsmul` -/

instance instZSMul [AddGroup Î²] [TopologicalAddGroup Î²] : SMul â„¤ C(Î±, Î²) where
  smul z f := âŸ¨z â€¢ â‡‘f, f.continuous.zsmul zâŸ©
#align continuous_map.has_zsmul ContinuousMap.instZSMul

@[to_additive existing]
instance instZPow [Group Î²] [TopologicalGroup Î²] : Pow C(Î±, Î²) â„¤ where
  pow f z := âŸ¨(â‡‘f) ^ z, f.continuous.zpow zâŸ©
#align continuous_map.has_zpow ContinuousMap.instZPow

@[to_additive (attr := norm_cast) (reorder := 7 8)]
theorem coe_zpow [Group Î²] [TopologicalGroup Î²] (f : C(Î±, Î²)) (z : â„¤) : â‡‘(f ^ z) = (â‡‘f) ^ z :=
  rfl
#align continuous_map.coe_zpow ContinuousMap.coe_zpow
#align continuous_map.coe_zsmul ContinuousMap.coe_zsmul

@[to_additive]
theorem zpow_apply [Group Î²] [TopologicalGroup Î²] (f : C(Î±, Î²)) (z : â„¤) (x : Î±) :
    (f ^ z) x = f x ^ z :=
  rfl
#align continuous_map.zpow_apply ContinuousMap.zpow_apply
#align continuous_map.zsmul_apply ContinuousMap.zsmul_apply

-- don't make auto-generated `coe_zsmul` and `zsmul_apply` simp as the linter complains they're
-- redundant WRT `coe_smul`
attribute [simp] coe_zpow zpow_apply

@[to_additive]
theorem zpow_comp [Group Î³] [TopologicalGroup Î³] (f : C(Î², Î³)) (z : â„¤) (g : C(Î±, Î²)) :
    (f ^ z).comp g = f.comp g ^ z :=
  rfl
#align continuous_map.zpow_comp ContinuousMap.zpow_comp
#align continuous_map.zsmul_comp ContinuousMap.zsmul_comp

-- don't make `zsmul_comp` simp as the linter complains it's redundant WRT `smul_comp`
attribute [simp] zpow_comp

end ContinuousMap

section GroupStructure

/-!
### Group structure

In this section we show that continuous functions valued in a topological group inherit
the structure of a group.
-/


section Subtype

/-- The `Submonoid` of continuous maps `Î± â†’ Î²`. -/
@[to_additive "The `AddSubmonoid` of continuous maps `Î± â†’ Î²`. "]
def continuousSubmonoid (Î± : Type*) (Î² : Type*) [TopologicalSpace Î±] [TopologicalSpace Î²]
    [MulOneClass Î²] [ContinuousMul Î²] : Submonoid (Î± â†’ Î²) where
  carrier := { f : Î± â†’ Î² | Continuous f }
  one_mem' := @continuous_const _ _ _ _ 1
  mul_mem' fc gc := fc.mul gc
#align continuous_submonoid continuousSubmonoid
#align continuous_add_submonoid continuousAddSubmonoid

/-- The subgroup of continuous maps `Î± â†’ Î²`. -/
@[to_additive "The `AddSubgroup` of continuous maps `Î± â†’ Î²`. "]
def continuousSubgroup (Î± : Type*) (Î² : Type*) [TopologicalSpace Î±] [TopologicalSpace Î²] [Group Î²]
    [TopologicalGroup Î²] : Subgroup (Î± â†’ Î²) :=
  { continuousSubmonoid Î± Î² with inv_mem' := fun fc => Continuous.inv fc }
#align continuous_subgroup continuousSubgroup
#align continuous_add_subgroup continuousAddSubgroup

end Subtype

namespace ContinuousMap

variable {Î± Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²]

@[to_additive]
instance [Semigroup Î²] [ContinuousMul Î²] : Semigroup C(Î±, Î²) :=
  coe_injective.semigroup _ coe_mul

@[to_additive]
instance [CommSemigroup Î²] [ContinuousMul Î²] : CommSemigroup C(Î±, Î²) :=
  coe_injective.commSemigroup _ coe_mul

@[to_additive]
instance [MulOneClass Î²] [ContinuousMul Î²] : MulOneClass C(Î±, Î²) :=
  coe_injective.mulOneClass _ coe_one coe_mul

instance [MulZeroClass Î²] [ContinuousMul Î²] : MulZeroClass C(Î±, Î²) :=
  coe_injective.mulZeroClass _ coe_zero coe_mul

instance [SemigroupWithZero Î²] [ContinuousMul Î²] : SemigroupWithZero C(Î±, Î²) :=
  coe_injective.semigroupWithZero _ coe_zero coe_mul

@[to_additive]
instance [Monoid Î²] [ContinuousMul Î²] : Monoid C(Î±, Î²) :=
  coe_injective.monoid _ coe_one coe_mul coe_pow

instance [MonoidWithZero Î²] [ContinuousMul Î²] : MonoidWithZero C(Î±, Î²) :=
  coe_injective.monoidWithZero _ coe_zero coe_one coe_mul coe_pow

@[to_additive]
instance [CommMonoid Î²] [ContinuousMul Î²] : CommMonoid C(Î±, Î²) :=
  coe_injective.commMonoid _ coe_one coe_mul coe_pow

instance [CommMonoidWithZero Î²] [ContinuousMul Î²] : CommMonoidWithZero C(Î±, Î²) :=
  coe_injective.commMonoidWithZero _ coe_zero coe_one coe_mul coe_pow

@[to_additive]
instance [LocallyCompactSpace Î±] [Mul Î²] [ContinuousMul Î²] : ContinuousMul C(Î±, Î²) :=
  âŸ¨by
    refine continuous_of_continuous_uncurry _ ?_
    have h1 : Continuous fun x : (C(Î±, Î²) Ã— C(Î±, Î²)) Ã— Î± => x.fst.fst x.snd :=
      continuous_eval.comp (continuous_fst.prod_map continuous_id)
    have h2 : Continuous fun x : (C(Î±, Î²) Ã— C(Î±, Î²)) Ã— Î± => x.fst.snd x.snd :=
      continuous_eval.comp (continuous_snd.prod_map continuous_id)
    exact h1.mul h2âŸ©

/-- Coercion to a function as a `MonoidHom`. Similar to `MonoidHom.coeFn`. -/
@[to_additive (attr := simps)
  "Coercion to a function as an `AddMonoidHom`. Similar to `AddMonoidHom.coeFn`." ]
def coeFnMonoidHom [Monoid Î²] [ContinuousMul Î²] : C(Î±, Î²) â†’* Î± â†’ Î² where
  toFun f := f
  map_one' := coe_one
  map_mul' := coe_mul
#align continuous_map.coe_fn_monoid_hom ContinuousMap.coeFnMonoidHom
#align continuous_map.coe_fn_add_monoid_hom ContinuousMap.coeFnAddMonoidHom

variable (Î±)

/-- Composition on the left by a (continuous) homomorphism of topological monoids, as a
`MonoidHom`. Similar to `MonoidHom.compLeft`. -/
@[to_additive (attr := simps)
"Composition on the left by a (continuous) homomorphism of topological `AddMonoid`s, as an
`AddMonoidHom`. Similar to `AddMonoidHom.comp_left`."]
protected def _root_.MonoidHom.compLeftContinuous {Î³ : Type*} [Monoid Î²] [ContinuousMul Î²]
    [TopologicalSpace Î³] [Monoid Î³] [ContinuousMul Î³] (g : Î² â†’* Î³) (hg : Continuous g) :
    C(Î±, Î²) â†’* C(Î±, Î³) where
  toFun f := (âŸ¨g, hgâŸ© : C(Î², Î³)).comp f
  map_one' := ext fun _ => g.map_one
  map_mul' _ _ := ext fun _ => g.map_mul _ _
#align monoid_hom.comp_left_continuous MonoidHom.compLeftContinuous
#align add_monoid_hom.comp_left_continuous AddMonoidHom.compLeftContinuous

variable {Î±}

/-- Composition on the right as a `MonoidHom`. Similar to `MonoidHom.compHom'`. -/
@[to_additive (attr := simps)
      "Composition on the right as an `AddMonoidHom`. Similar to `AddMonoidHom.compHom'`."]
def compMonoidHom' {Î³ : Type*} [TopologicalSpace Î³] [MulOneClass Î³] [ContinuousMul Î³]
    (g : C(Î±, Î²)) : C(Î², Î³) â†’* C(Î±, Î³) where
  toFun f := f.comp g
  map_one' := one_comp g
  map_mul' fâ‚ fâ‚‚ := mul_comp fâ‚ fâ‚‚ g
#align continuous_map.comp_monoid_hom' ContinuousMap.compMonoidHom'
#align continuous_map.comp_add_monoid_hom' ContinuousMap.compAddMonoidHom'

@[to_additive (attr := simp)]
theorem coe_prod [CommMonoid Î²] [ContinuousMul Î²] {Î¹ : Type*} (s : Finset Î¹) (f : Î¹ â†’ C(Î±, Î²)) :
    â‡‘(âˆ i âˆˆ s, f i) = âˆ i âˆˆ s, (f i : Î± â†’ Î²) :=
  map_prod coeFnMonoidHom f s
#align continuous_map.coe_prod ContinuousMap.coe_prod
#align continuous_map.coe_sum ContinuousMap.coe_sum

@[to_additive]
theorem prod_apply [CommMonoid Î²] [ContinuousMul Î²] {Î¹ : Type*} (s : Finset Î¹) (f : Î¹ â†’ C(Î±, Î²))
    (a : Î±) : (âˆ i âˆˆ s, f i) a = âˆ i âˆˆ s, f i a := by simp
#align continuous_map.prod_apply ContinuousMap.prod_apply
#align continuous_map.sum_apply ContinuousMap.sum_apply

@[to_additive]
instance [Group Î²] [TopologicalGroup Î²] : Group C(Î±, Î²) :=
  coe_injective.group _ coe_one coe_mul coe_inv coe_div coe_pow coe_zpow

@[to_additive]
instance instCommGroupContinuousMap [CommGroup Î²] [TopologicalGroup Î²] : CommGroup C(Î±, Î²) :=
  coe_injective.commGroup _ coe_one coe_mul coe_inv coe_div coe_pow coe_zpow

@[to_additive]
instance [CommGroup Î²] [TopologicalGroup Î²] : TopologicalGroup C(Î±, Î²) where
  continuous_mul := by
    letI : UniformSpace Î² := TopologicalGroup.toUniformSpace Î²
    have : UniformGroup Î² := comm_topologicalGroup_is_uniform
    rw [continuous_iff_continuousAt]
    rintro âŸ¨f, gâŸ©
    rw [ContinuousAt, tendsto_iff_forall_compact_tendstoUniformlyOn, nhds_prod_eq]
    exact fun K hK =>
      uniformContinuous_mul.comp_tendstoUniformlyOn
        ((tendsto_iff_forall_compact_tendstoUniformlyOn.mp Filter.tendsto_id K hK).prod
          (tendsto_iff_forall_compact_tendstoUniformlyOn.mp Filter.tendsto_id K hK))
  continuous_inv := by
    letI : UniformSpace Î² := TopologicalGroup.toUniformSpace Î²
    have : UniformGroup Î² := comm_topologicalGroup_is_uniform
    rw [continuous_iff_continuousAt]
    intro f
    rw [ContinuousAt, tendsto_iff_forall_compact_tendstoUniformlyOn]
    exact fun K hK =>
      uniformContinuous_inv.comp_tendstoUniformlyOn
        (tendsto_iff_forall_compact_tendstoUniformlyOn.mp Filter.tendsto_id K hK)

-- TODO: rewrite the next three lemmas for products and deduce sum case via `to_additive`, once
-- definition of `tprod` is in place
/-- If `Î±` is locally compact, and an infinite sum of functions in `C(Î±, Î²)`
converges to `g` (for the compact-open topology), then the pointwise sum converges to `g x` for
all `x âˆˆ Î±`. -/
theorem hasSum_apply {Î³ : Type*} [AddCommMonoid Î²] [ContinuousAdd Î²]
    {f : Î³ â†’ C(Î±, Î²)} {g : C(Î±, Î²)} (hf : HasSum f g) (x : Î±) :
    HasSum (fun i : Î³ => f i x) (g x) := by
  let ev : C(Î±, Î²) â†’+ Î² := (Pi.evalAddMonoidHom _ x).comp coeFnAddMonoidHom
  exact hf.map ev (ContinuousMap.continuous_eval_const x)
#align continuous_map.has_sum_apply ContinuousMap.hasSum_apply

theorem summable_apply [AddCommMonoid Î²] [ContinuousAdd Î²] {Î³ : Type*} {f : Î³ â†’ C(Î±, Î²)}
    (hf : Summable f) (x : Î±) : Summable fun i : Î³ => f i x :=
  (hasSum_apply hf.hasSum x).summable
#align continuous_map.summable_apply ContinuousMap.summable_apply

theorem tsum_apply [T2Space Î²] [AddCommMonoid Î²] [ContinuousAdd Î²] {Î³ : Type*} {f : Î³ â†’ C(Î±, Î²)}
    (hf : Summable f) (x : Î±) :
    âˆ‘' i : Î³, f i x = (âˆ‘' i : Î³, f i) x :=
  (hasSum_apply hf.hasSum x).tsum_eq
#align continuous_map.tsum_apply ContinuousMap.tsum_apply

end ContinuousMap

end GroupStructure

section RingStructure

/-!
### Ring structure

In this section we show that continuous functions valued in a topological semiring `R` inherit
the structure of a ring.
-/


section Subtype

/-- The subsemiring of continuous maps `Î± â†’ Î²`. -/
def continuousSubsemiring (Î± : Type*) (R : Type*) [TopologicalSpace Î±] [TopologicalSpace R]
    [NonAssocSemiring R] [TopologicalSemiring R] : Subsemiring (Î± â†’ R) :=
  { continuousAddSubmonoid Î± R, continuousSubmonoid Î± R with }
#align continuous_subsemiring continuousSubsemiring

/-- The subring of continuous maps `Î± â†’ Î²`. -/
def continuousSubring (Î± : Type*) (R : Type*) [TopologicalSpace Î±] [TopologicalSpace R] [Ring R]
    [TopologicalRing R] : Subring (Î± â†’ R) :=
  { continuousAddSubgroup Î± R, continuousSubsemiring Î± R with }
#align continuous_subring continuousSubring

end Subtype

namespace ContinuousMap

instance {Î± : Type*} {Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²]
    [NonUnitalNonAssocSemiring Î²] [TopologicalSemiring Î²] : NonUnitalNonAssocSemiring C(Î±, Î²) :=
  coe_injective.nonUnitalNonAssocSemiring _ coe_zero coe_add coe_mul coe_nsmul

instance {Î± : Type*} {Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²] [NonUnitalSemiring Î²]
    [TopologicalSemiring Î²] : NonUnitalSemiring C(Î±, Î²) :=
  coe_injective.nonUnitalSemiring _ coe_zero coe_add coe_mul coe_nsmul

instance {Î± : Type*} {Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²] [AddMonoidWithOne Î²]
    [ContinuousAdd Î²] : AddMonoidWithOne C(Î±, Î²) :=
  coe_injective.addMonoidWithOne _ coe_zero coe_one coe_add coe_nsmul coe_natCast

instance {Î± : Type*} {Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²] [NonAssocSemiring Î²]
    [TopologicalSemiring Î²] : NonAssocSemiring C(Î±, Î²) :=
  coe_injective.nonAssocSemiring _ coe_zero coe_one coe_add coe_mul coe_nsmul coe_natCast

instance {Î± : Type*} {Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²] [Semiring Î²]
    [TopologicalSemiring Î²] : Semiring C(Î±, Î²) :=
  coe_injective.semiring _ coe_zero coe_one coe_add coe_mul coe_nsmul coe_pow coe_natCast

instance {Î± : Type*} {Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²]
    [NonUnitalNonAssocRing Î²] [TopologicalRing Î²] : NonUnitalNonAssocRing C(Î±, Î²) :=
  coe_injective.nonUnitalNonAssocRing _ coe_zero coe_add coe_mul coe_neg coe_sub coe_nsmul coe_zsmul

instance {Î± : Type*} {Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²] [NonUnitalRing Î²]
    [TopologicalRing Î²] : NonUnitalRing C(Î±, Î²) :=
  coe_injective.nonUnitalRing _ coe_zero coe_add coe_mul coe_neg coe_sub coe_nsmul coe_zsmul

instance {Î± : Type*} {Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²] [NonAssocRing Î²]
    [TopologicalRing Î²] : NonAssocRing C(Î±, Î²) :=
  coe_injective.nonAssocRing _ coe_zero coe_one coe_add coe_mul coe_neg coe_sub coe_nsmul coe_zsmul
    coe_natCast coe_intCast

instance instRingContinuousMap {Î± : Type*} {Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²]
    [Ring Î²] [TopologicalRing Î²] : Ring C(Î±, Î²) :=
  coe_injective.ring _ coe_zero coe_one coe_add coe_mul coe_neg coe_sub coe_nsmul coe_zsmul coe_pow
    coe_natCast coe_intCast

instance {Î± : Type*} {Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²]
    [NonUnitalCommSemiring Î²] [TopologicalSemiring Î²] : NonUnitalCommSemiring C(Î±, Î²) :=
  coe_injective.nonUnitalCommSemiring _ coe_zero coe_add coe_mul coe_nsmul

instance {Î± : Type*} {Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²] [CommSemiring Î²]
    [TopologicalSemiring Î²] : CommSemiring C(Î±, Î²) :=
  coe_injective.commSemiring _ coe_zero coe_one coe_add coe_mul coe_nsmul coe_pow coe_natCast

instance {Î± : Type*} {Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²] [NonUnitalCommRing Î²]
    [TopologicalRing Î²] : NonUnitalCommRing C(Î±, Î²) :=
  coe_injective.nonUnitalCommRing _ coe_zero coe_add coe_mul coe_neg coe_sub coe_nsmul coe_zsmul

instance {Î± : Type*} {Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²] [CommRing Î²]
    [TopologicalRing Î²] : CommRing C(Î±, Î²) :=
  coe_injective.commRing _ coe_zero coe_one coe_add coe_mul coe_neg coe_sub coe_nsmul coe_zsmul
    coe_pow coe_natCast coe_intCast

/-- Composition on the left by a (continuous) homomorphism of topological semirings, as a
`RingHom`.  Similar to `RingHom.compLeft`. -/
@[simps!]
protected def _root_.RingHom.compLeftContinuous (Î± : Type*) {Î² : Type*} {Î³ : Type*}
    [TopologicalSpace Î±]
    [TopologicalSpace Î²] [Semiring Î²] [TopologicalSemiring Î²] [TopologicalSpace Î³] [Semiring Î³]
    [TopologicalSemiring Î³] (g : Î² â†’+* Î³) (hg : Continuous g) : C(Î±, Î²) â†’+* C(Î±, Î³) :=
  { g.toMonoidHom.compLeftContinuous Î± hg, g.toAddMonoidHom.compLeftContinuous Î± hg with }
#align ring_hom.comp_left_continuous RingHom.compLeftContinuous

/-- Coercion to a function as a `RingHom`. -/
@[simps!]
def coeFnRingHom {Î± : Type*} {Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²] [Semiring Î²]
    [TopologicalSemiring Î²] : C(Î±, Î²) â†’+* Î± â†’ Î² :=
  { (coeFnMonoidHom : C(Î±, Î²) â†’* _),
    (coeFnAddMonoidHom : C(Î±, Î²) â†’+ _) with }
#align continuous_map.coe_fn_ring_hom ContinuousMap.coeFnRingHom

end ContinuousMap

end RingStructure

attribute [local ext] Subtype.eq

section ModuleStructure

/-!
### Module structure

In this section we show that continuous functions valued in a topological module `M` over a
topological semiring `R` inherit the structure of a module.
-/


section Subtype

variable (Î± : Type*) [TopologicalSpace Î±]
variable (R : Type*) [Semiring R]
variable (M : Type*) [TopologicalSpace M] [AddCommGroup M]
variable [Module R M] [ContinuousConstSMul R M] [TopologicalAddGroup M]

/-- The `R`-submodule of continuous maps `Î± â†’ M`. -/
def continuousSubmodule : Submodule R (Î± â†’ M) :=
  { continuousAddSubgroup Î± M with
    carrier := { f : Î± â†’ M | Continuous f }
    smul_mem' := fun c _ hf => hf.const_smul c }
#align continuous_submodule continuousSubmodule

end Subtype

namespace ContinuousMap

variable {Î± Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²] {R Râ‚ : Type*} {M : Type*}
  [TopologicalSpace M] {Mâ‚‚ : Type*} [TopologicalSpace Mâ‚‚]

@[to_additive]
instance instSMul [SMul R M] [ContinuousConstSMul R M] : SMul R C(Î±, M) :=
  âŸ¨fun r f => âŸ¨r â€¢ â‡‘f, f.continuous.const_smul râŸ©âŸ©
#align continuous_map.has_smul ContinuousMap.instSMul
#align continuous_map.has_vadd ContinuousMap.instVAdd

@[to_additive]
instance [LocallyCompactSpace Î±] [SMul R M] [ContinuousConstSMul R M] :
    ContinuousConstSMul R C(Î±, M) :=
  âŸ¨fun Î³ => continuous_of_continuous_uncurry _ (continuous_eval.const_smul Î³)âŸ©

@[to_additive]
instance [LocallyCompactSpace Î±] [TopologicalSpace R] [SMul R M] [ContinuousSMul R M] :
    ContinuousSMul R C(Î±, M) :=
  âŸ¨by
    refine continuous_of_continuous_uncurry _ ?_
    have h : Continuous fun x : (R Ã— C(Î±, M)) Ã— Î± => x.fst.snd x.snd :=
      continuous_eval.comp (continuous_snd.prod_map continuous_id)
    exact (continuous_fst.comp continuous_fst).smul hâŸ©

@[to_additive (attr := simp, norm_cast)]
theorem coe_smul [SMul R M] [ContinuousConstSMul R M] (c : R) (f : C(Î±, M)) : â‡‘(c â€¢ f) = c â€¢ â‡‘f :=
  rfl
#align continuous_map.coe_smul ContinuousMap.coe_smul
#align continuous_map.coe_vadd ContinuousMap.coe_vadd

@[to_additive]
theorem smul_apply [SMul R M] [ContinuousConstSMul R M] (c : R) (f : C(Î±, M)) (a : Î±) :
    (c â€¢ f) a = c â€¢ f a :=
  rfl
#align continuous_map.smul_apply ContinuousMap.smul_apply
#align continuous_map.vadd_apply ContinuousMap.vadd_apply

@[to_additive (attr := simp)]
theorem smul_comp [SMul R M] [ContinuousConstSMul R M] (r : R) (f : C(Î², M)) (g : C(Î±, Î²)) :
    (r â€¢ f).comp g = r â€¢ f.comp g :=
  rfl
#align continuous_map.smul_comp ContinuousMap.smul_comp
#align continuous_map.vadd_comp ContinuousMap.vadd_comp

@[to_additive]
instance [SMul R M] [ContinuousConstSMul R M] [SMul Râ‚ M] [ContinuousConstSMul Râ‚ M]
    [SMulCommClass R Râ‚ M] : SMulCommClass R Râ‚ C(Î±, M) where
  smul_comm _ _ _ := ext fun _ => smul_comm _ _ _

instance [SMul R M] [ContinuousConstSMul R M] [SMul Râ‚ M] [ContinuousConstSMul Râ‚ M] [SMul R Râ‚]
    [IsScalarTower R Râ‚ M] : IsScalarTower R Râ‚ C(Î±, M) where
  smul_assoc _ _ _ := ext fun _ => smul_assoc _ _ _

instance [SMul R M] [SMul Ráµáµ’áµ– M] [ContinuousConstSMul R M] [IsCentralScalar R M] :
    IsCentralScalar R C(Î±, M) where op_smul_eq_smul _ _ := ext fun _ => op_smul_eq_smul _ _

instance [Monoid R] [MulAction R M] [ContinuousConstSMul R M] : MulAction R C(Î±, M) :=
  Function.Injective.mulAction _ coe_injective coe_smul

instance [Monoid R] [AddMonoid M] [DistribMulAction R M] [ContinuousAdd M]
    [ContinuousConstSMul R M] : DistribMulAction R C(Î±, M) :=
  Function.Injective.distribMulAction coeFnAddMonoidHom coe_injective coe_smul

variable [Semiring R] [AddCommMonoid M] [AddCommMonoid Mâ‚‚]
variable [ContinuousAdd M] [Module R M] [ContinuousConstSMul R M]
variable [ContinuousAdd Mâ‚‚] [Module R Mâ‚‚] [ContinuousConstSMul R Mâ‚‚]

instance module : Module R C(Î±, M) :=
  Function.Injective.module R coeFnAddMonoidHom coe_injective coe_smul
#align continuous_map.module ContinuousMap.module

variable (R)

/-- Composition on the left by a continuous linear map, as a `LinearMap`.
Similar to `LinearMap.compLeft`. -/
@[simps]
protected def _root_.ContinuousLinearMap.compLeftContinuous (Î± : Type*) [TopologicalSpace Î±]
    (g : M â†’L[R] Mâ‚‚) : C(Î±, M) â†’â‚—[R] C(Î±, Mâ‚‚) :=
  { g.toLinearMap.toAddMonoidHom.compLeftContinuous Î± g.continuous with
    map_smul' := fun c _ => ext fun _ => g.map_smul' c _ }
#align continuous_linear_map.comp_left_continuous ContinuousLinearMap.compLeftContinuous

/-- Coercion to a function as a `LinearMap`. -/
@[simps]
def coeFnLinearMap : C(Î±, M) â†’â‚—[R] Î± â†’ M :=
  { (coeFnAddMonoidHom : C(Î±, M) â†’+ _) with
    map_smul' := coe_smul }
#align continuous_map.coe_fn_linear_map ContinuousMap.coeFnLinearMap

end ContinuousMap

end ModuleStructure

section AlgebraStructure

/-!
### Algebra structure

In this section we show that continuous functions valued in a topological algebra `A` over a ring
`R` inherit the structure of an algebra. Note that the hypothesis that `A` is a topological algebra
is obtained by requiring that `A` be both a `ContinuousSMul` and a `TopologicalSemiring`. -/


section Subtype

variable {Î± : Type*} [TopologicalSpace Î±] {R : Type*} [CommSemiring R] {A : Type*}
  [TopologicalSpace A] [Semiring A] [Algebra R A] [TopologicalSemiring A]

/-- The `R`-subalgebra of continuous maps `Î± â†’ A`. -/
def continuousSubalgebra : Subalgebra R (Î± â†’ A) :=
  { continuousSubsemiring Î± A with
    carrier := { f : Î± â†’ A | Continuous f }
    algebraMap_mem' := fun r => (continuous_const : Continuous fun _ : Î± => algebraMap R A r) }
#align continuous_subalgebra continuousSubalgebra

end Subtype

section ContinuousMap

variable {Î± : Type*} [TopologicalSpace Î±] {R : Type*} [CommSemiring R] {A : Type*}
  [TopologicalSpace A] [Semiring A] [Algebra R A] [TopologicalSemiring A] {Aâ‚‚ : Type*}
  [TopologicalSpace Aâ‚‚] [Semiring Aâ‚‚] [Algebra R Aâ‚‚] [TopologicalSemiring Aâ‚‚]

/-- Continuous constant functions as a `RingHom`. -/
def ContinuousMap.C : R â†’+* C(Î±, A) where
  toFun := fun c : R => âŸ¨fun _ : Î± => (algebraMap R A) c, continuous_constâŸ©
  map_one' := by ext _; exact (algebraMap R A).map_one
  map_mul' câ‚ câ‚‚ := by ext _; exact (algebraMap R A).map_mul _ _
  map_zero' := by ext _; exact (algebraMap R A).map_zero
  map_add' câ‚ câ‚‚ := by ext _; exact (algebraMap R A).map_add _ _
set_option linter.uppercaseLean3 false in
#align continuous_map.C ContinuousMap.C

@[simp]
theorem ContinuousMap.C_apply (r : R) (a : Î±) : ContinuousMap.C r a = algebraMap R A r :=
  rfl
set_option linter.uppercaseLean3 false in
#align continuous_map.C_apply ContinuousMap.C_apply

instance ContinuousMap.algebra : Algebra R C(Î±, A) where
  toRingHom := ContinuousMap.C
  commutes' c f := by ext x; exact Algebra.commutes' _ _
  smul_def' c f := by ext x; exact Algebra.smul_def' _ _
#align continuous_map.algebra ContinuousMap.algebra

variable (R)

/-- Composition on the left by a (continuous) homomorphism of topological `R`-algebras, as an
`AlgHom`. Similar to `AlgHom.compLeft`. -/
@[simps!]
protected def AlgHom.compLeftContinuous {Î± : Type*} [TopologicalSpace Î±] (g : A â†’â‚[R] Aâ‚‚)
    (hg : Continuous g) : C(Î±, A) â†’â‚[R] C(Î±, Aâ‚‚) :=
  { g.toRingHom.compLeftContinuous Î± hg with
    commutes' := fun _ => ContinuousMap.ext fun _ => g.commutes' _ }
#align alg_hom.comp_left_continuous AlgHom.compLeftContinuous

variable (A)

/-- Precomposition of functions into a topological semiring by a continuous map is an algebra
homomorphism. -/
@[simps]
def ContinuousMap.compRightAlgHom {Î± Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²]
    (f : C(Î±, Î²)) : C(Î², A) â†’â‚[R] C(Î±, A) where
  toFun g := g.comp f
  map_zero' := ext fun _ â†¦ rfl
  map_add'  _ _ := ext fun _ â†¦ rfl
  map_one' := ext fun _ â†¦ rfl
  map_mul' _ _ := ext fun _ â†¦ rfl
  commutes' _ := ext fun _ â†¦ rfl
#align continuous_map.comp_right_alg_hom ContinuousMap.compRightAlgHom

variable {A}

/-- Coercion to a function as an `AlgHom`. -/
@[simps!]
def ContinuousMap.coeFnAlgHom : C(Î±, A) â†’â‚[R] Î± â†’ A :=
  { (ContinuousMap.coeFnRingHom : C(Î±, A) â†’+* _) with
    commutes' := fun _ => rfl }
#align continuous_map.coe_fn_alg_hom ContinuousMap.coeFnAlgHom

variable {R}

/-- A version of `Set.SeparatesPoints` for subalgebras of the continuous functions,
used for stating the Stone-Weierstrass theorem.
-/
abbrev Subalgebra.SeparatesPoints (s : Subalgebra R C(Î±, A)) : Prop :=
  Set.SeparatesPoints ((fun f : C(Î±, A) => (f : Î± â†’ A)) '' (s : Set C(Î±, A)))
#align subalgebra.separates_points Subalgebra.SeparatesPoints

theorem Subalgebra.separatesPoints_monotone :
    Monotone fun s : Subalgebra R C(Î±, A) => s.SeparatesPoints := fun s s' r h x y n => by
  obtain âŸ¨f, m, wâŸ© := h n
  rcases m with âŸ¨f, âŸ¨m, rflâŸ©âŸ©
  exact âŸ¨_, âŸ¨f, âŸ¨r m, rflâŸ©âŸ©, wâŸ©
#align subalgebra.separates_points_monotone Subalgebra.separatesPoints_monotone

@[simp]
theorem algebraMap_apply (k : R) (a : Î±) : algebraMap R C(Î±, A) k a = k â€¢ (1 : A) := by
  rw [Algebra.algebraMap_eq_smul_one]
  rfl
#align algebra_map_apply algebraMap_apply

variable {ð•œ : Type*} [TopologicalSpace ð•œ]
variable (s : Set C(Î±, ð•œ)) (f : s) (x : Î±)

/-- A set of continuous maps "separates points strongly"
if for each pair of distinct points there is a function with specified values on them.

We give a slightly unusual formulation, where the specified values are given by some
function `v`, and we ask `f x = v x âˆ§ f y = v y`. This avoids needing a hypothesis `x â‰  y`.

In fact, this definition would work perfectly well for a set of non-continuous functions,
but as the only current use case is in the Stone-Weierstrass theorem,
writing it this way avoids having to deal with casts inside the set.
(This may need to change if we do Stone-Weierstrass on non-compact spaces,
where the functions would be continuous functions vanishing at infinity.)
-/
def Set.SeparatesPointsStrongly (s : Set C(Î±, ð•œ)) : Prop :=
  âˆ€ (v : Î± â†’ ð•œ) (x y : Î±), âˆƒ f âˆˆ s, (f x : ð•œ) = v x âˆ§ f y = v y
#align set.separates_points_strongly Set.SeparatesPointsStrongly

variable [Field ð•œ] [TopologicalRing ð•œ]

/-- Working in continuous functions into a topological field,
a subalgebra of functions that separates points also separates points strongly.

By the hypothesis, we can find a function `f` so `f x â‰  f y`.
By an affine transformation in the field we can arrange so that `f x = a` and `f x = b`.
-/
theorem Subalgebra.SeparatesPoints.strongly {s : Subalgebra ð•œ C(Î±, ð•œ)} (h : s.SeparatesPoints) :
    (s : Set C(Î±, ð•œ)).SeparatesPointsStrongly := fun v x y => by
  by_cases n : x = y
  Â· subst n
    exact âŸ¨_, (v x â€¢ (1 : s) : s).prop, mul_one _, mul_one _âŸ©
  obtain âŸ¨_, âŸ¨f, hf, rflâŸ©, hxyâŸ© := h n
  replace hxy : f x - f y â‰  0 := sub_ne_zero_of_ne hxy
  let a := v x
  let b := v y
  let f' : s :=
    ((b - a) * (f x - f y)â»Â¹) â€¢ (algebraMap _ s (f x) - (âŸ¨f, hfâŸ© : s)) + algebraMap _ s a
  refine âŸ¨f', f'.prop, ?_, ?_âŸ©
  Â· simp [f']
  Â· simp [f', inv_mul_cancel_rightâ‚€ hxy]
#align subalgebra.separates_points.strongly Subalgebra.SeparatesPoints.strongly

end ContinuousMap

instance ContinuousMap.subsingleton_subalgebra (Î± : Type*) [TopologicalSpace Î±] (R : Type*)
    [CommSemiring R] [TopologicalSpace R] [TopologicalSemiring R] [Subsingleton Î±] :
    Subsingleton (Subalgebra R C(Î±, R)) :=
  âŸ¨fun sâ‚ sâ‚‚ => by
    cases isEmpty_or_nonempty Î±
    Â· haveI : Subsingleton C(Î±, R) := DFunLike.coe_injective.subsingleton
      exact Subsingleton.elim _ _
    Â· inhabit Î±
      ext f
      have h : f = algebraMap R C(Î±, R) (f default) := by
        ext x'
        simp only [mul_one, Algebra.id.smul_eq_mul, algebraMap_apply]
        congr
        simp [eq_iff_true_of_subsingleton]
      rw [h]
      simp only [Subalgebra.algebraMap_mem]âŸ©
#align continuous_map.subsingleton_subalgebra ContinuousMap.subsingleton_subalgebra

end AlgebraStructure

section ModuleOverContinuousFunctions

/-!
### Structure as module over scalar functions

If `M` is a module over `R`, then we show that the space of continuous functions from `Î±` to `M`
is naturally a module over the ring of continuous functions from `Î±` to `R`. -/


namespace ContinuousMap

instance instSMul' {Î± : Type*} [TopologicalSpace Î±] {R : Type*} [Semiring R] [TopologicalSpace R]
    {M : Type*} [TopologicalSpace M] [AddCommMonoid M] [Module R M] [ContinuousSMul R M] :
    SMul C(Î±, R) C(Î±, M) :=
  âŸ¨fun f g => âŸ¨fun x => f x â€¢ g x, Continuous.smul f.2 g.2âŸ©âŸ©
#align continuous_map.has_smul' ContinuousMap.instSMul'

instance module' {Î± : Type*} [TopologicalSpace Î±] (R : Type*) [Semiring R] [TopologicalSpace R]
    [TopologicalSemiring R] (M : Type*) [TopologicalSpace M] [AddCommMonoid M] [ContinuousAdd M]
    [Module R M] [ContinuousSMul R M] : Module C(Î±, R) C(Î±, M) where
  smul := (Â· â€¢ Â·)
  smul_add c f g := by ext x; exact smul_add (c x) (f x) (g x)
  add_smul câ‚ câ‚‚ f := by ext x; exact add_smul (câ‚ x) (câ‚‚ x) (f x)
  mul_smul câ‚ câ‚‚ f := by ext x; exact mul_smul (câ‚ x) (câ‚‚ x) (f x)
  one_smul f := by ext x; exact one_smul R (f x)
  zero_smul f := by ext x; exact zero_smul _ _
  smul_zero r := by ext x; exact smul_zero _
#align continuous_map.module' ContinuousMap.module'

end ContinuousMap

end ModuleOverContinuousFunctions

/-!
We now provide formulas for `f âŠ“ g` and `f âŠ” g`, where `f g : C(Î±, Î²)`,
in terms of `ContinuousMap.abs`.
-/

namespace ContinuousMap

section Lattice

variable {Î± : Type*} [TopologicalSpace Î±]
variable {Î² : Type*} [TopologicalSpace Î²]

/-! `C(Î±, Î²)`is a lattice ordered group -/

@[to_additive]
instance instCovariantClass_mul_le_left [PartialOrder Î²] [Mul Î²] [ContinuousMul Î²]
    [CovariantClass Î² Î² (Â· * Â·) (Â· â‰¤ Â·)] :
    CovariantClass C(Î±, Î²) C(Î±, Î²) (Â· * Â·) (Â· â‰¤ Â·) :=
  âŸ¨fun _ _ _ hgâ‚â‚‚ x => mul_le_mul_left' (hgâ‚â‚‚ x) _âŸ©

@[to_additive]
instance instCovariantClass_mul_le_right [PartialOrder Î²] [Mul Î²] [ContinuousMul Î²]
    [CovariantClass Î² Î² (Function.swap (Â· * Â·)) (Â· â‰¤ Â·)] :
    CovariantClass C(Î±, Î²) C(Î±, Î²) (Function.swap (Â· * Â·)) (Â· â‰¤ Â·) :=
  âŸ¨fun _ _ _ hgâ‚â‚‚ x => mul_le_mul_right' (hgâ‚â‚‚ x) _âŸ©

variable [Group Î²] [TopologicalGroup Î²] [Lattice Î²] [TopologicalLattice Î²]

@[to_additive (attr := simp, norm_cast)]
lemma coe_mabs (f : C(Î±, Î²)) : â‡‘|f|â‚˜ = |â‡‘f|â‚˜ := rfl

@[to_additive (attr := simp)]
lemma mabs_apply (f : C(Î±, Î²)) (x : Î±) : |f|â‚˜ x = |f x|â‚˜ := rfl
#align continuous_map.abs_apply ContinuousMap.abs_apply

end Lattice

/-!
### Star structure

If `Î²` has a continuous star operation, we put a star structure on `C(Î±, Î²)` by using the
star operation pointwise.

If `Î²` is a â‹†-ring, then `C(Î±, Î²)` inherits a â‹†-ring structure.

If `Î²` is a â‹†-ring and a â‹†-module over `R`, then the space of continuous functions from `Î±` to `Î²`
is a â‹†-module over `R`.

-/


section StarStructure

variable {R Î± Î² : Type*}
variable [TopologicalSpace Î±] [TopologicalSpace Î²]

section Star

variable [Star Î²] [ContinuousStar Î²]

instance : Star C(Î±, Î²) where star f := starContinuousMap.comp f

@[simp]
theorem coe_star (f : C(Î±, Î²)) : â‡‘(star f) = star (â‡‘f) :=
  rfl
#align continuous_map.coe_star ContinuousMap.coe_star

@[simp]
theorem star_apply (f : C(Î±, Î²)) (x : Î±) : star f x = star (f x) :=
  rfl
#align continuous_map.star_apply ContinuousMap.star_apply

instance instTrivialStar [TrivialStar Î²] : TrivialStar C(Î±, Î²) where
  star_trivial _ := ext fun _ => star_trivial _

end Star

instance [InvolutiveStar Î²] [ContinuousStar Î²] : InvolutiveStar C(Î±, Î²) where
  star_involutive _ := ext fun _ => star_star _

instance starAddMonoid [AddMonoid Î²] [ContinuousAdd Î²] [StarAddMonoid Î²] [ContinuousStar Î²] :
    StarAddMonoid C(Î±, Î²) where
  star_add _ _ := ext fun _ => star_add _ _

instance starMul [Mul Î²] [ContinuousMul Î²] [StarMul Î²] [ContinuousStar Î²] :
    StarMul C(Î±, Î²) where
  star_mul _ _ := ext fun _ => star_mul _ _

instance [NonUnitalNonAssocSemiring Î²] [TopologicalSemiring Î²] [StarRing Î²] [ContinuousStar Î²] :
    StarRing C(Î±, Î²) :=
  { ContinuousMap.starAddMonoid, ContinuousMap.starMul with }

instance [Star R] [Star Î²] [SMul R Î²] [StarModule R Î²] [ContinuousStar Î²]
    [ContinuousConstSMul R Î²] : StarModule R C(Î±, Î²) where
  star_smul _ _ := ext fun _ => star_smul _ _

end StarStructure

section Precomposition

variable {X Y Z : Type*} [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]
variable (ð•œ : Type*) [CommSemiring ð•œ]
variable (A : Type*) [TopologicalSpace A] [Semiring A] [TopologicalSemiring A] [Star A]
variable [ContinuousStar A] [Algebra ð•œ A]

/-- The functorial map taking `f : C(X, Y)` to `C(Y, A) â†’â‹†â‚[ð•œ] C(X, A)` given by pre-composition
with the continuous function `f`. See `ContinuousMap.compMonoidHom'` and
`ContinuousMap.compAddMonoidHom'`, `ContinuousMap.compRightAlgHom` for bundlings of
pre-composition into a `MonoidHom`, an `AddMonoidHom` and an `AlgHom`, respectively, under
suitable assumptions on `A`. -/
@[simps]
def compStarAlgHom' (f : C(X, Y)) : C(Y, A) â†’â‹†â‚[ð•œ] C(X, A) where
  toFun g := g.comp f
  map_one' := one_comp _
  map_mul' _ _ := rfl
  map_zero' := zero_comp f
  map_add' _ _ := rfl
  commutes' _ := rfl
  map_star' _ := rfl
#align continuous_map.comp_star_alg_hom' ContinuousMap.compStarAlgHom'

/-- `ContinuousMap.compStarAlgHom'` sends the identity continuous map to the identity
`StarAlgHom` -/
theorem compStarAlgHom'_id : compStarAlgHom' ð•œ A (ContinuousMap.id X) = StarAlgHom.id ð•œ C(X, A) :=
  StarAlgHom.ext fun _ => ContinuousMap.ext fun _ => rfl
#align continuous_map.comp_star_alg_hom'_id ContinuousMap.compStarAlgHom'_id

/-- `ContinuousMap.compStarAlgHom'` is functorial. -/
theorem compStarAlgHom'_comp (g : C(Y, Z)) (f : C(X, Y)) :
    compStarAlgHom' ð•œ A (g.comp f) = (compStarAlgHom' ð•œ A f).comp (compStarAlgHom' ð•œ A g) :=
  StarAlgHom.ext fun _ => ContinuousMap.ext fun _ => rfl
#align continuous_map.comp_star_alg_hom'_comp ContinuousMap.compStarAlgHom'_comp

end Precomposition

section Postcomposition

variable (X : Type*) {ð•œ A B C : Type*} [TopologicalSpace X] [CommSemiring ð•œ]
variable [TopologicalSpace A] [Semiring A] [TopologicalSemiring A] [Star A]
variable [ContinuousStar A] [Algebra ð•œ A]
variable [TopologicalSpace B] [Semiring B] [TopologicalSemiring B] [Star B]
variable [ContinuousStar B] [Algebra ð•œ B]
variable [TopologicalSpace C] [Semiring C] [TopologicalSemiring C] [Star C]
variable [ContinuousStar C] [Algebra ð•œ C]

/-- Post-composition with a continuous star algebra homomorphism is a star algebra homomorphism
between spaces of continuous maps. -/
@[simps]
def compStarAlgHom (Ï† : A â†’â‹†â‚[ð•œ] B) (hÏ† : Continuous Ï†) :
    C(X, A) â†’â‹†â‚[ð•œ] C(X, B) where
  toFun f := (âŸ¨Ï†, hÏ†âŸ© : C(A, B)).comp f
  map_one' := ext fun _ => map_one Ï†
  map_mul' f g := ext fun x => map_mul Ï† (f x) (g x)
  map_zero' := ext fun _ => map_zero Ï†
  map_add' f g := ext fun x => map_add Ï† (f x) (g x)
  commutes' r := ext fun _x => AlgHomClass.commutes Ï† r
  map_star' f := ext fun x => map_star Ï† (f x)

/-- `ContinuousMap.compStarAlgHom` sends the identity `StarAlgHom` on `A` to the identity
`StarAlgHom` on `C(X, A)`. -/
lemma compStarAlgHom_id : compStarAlgHom X (.id ð•œ A) continuous_id = .id ð•œ C(X, A) := rfl

/-- `ContinuousMap.compStarAlgHom` is functorial. -/
lemma compStarAlgHom_comp (Ï† : A â†’â‹†â‚[ð•œ] B) (Ïˆ : B â†’â‹†â‚[ð•œ] C) (hÏ† : Continuous Ï†)
    (hÏˆ : Continuous Ïˆ) : compStarAlgHom X (Ïˆ.comp Ï†) (hÏˆ.comp hÏ†) =
      (compStarAlgHom X Ïˆ hÏˆ).comp (compStarAlgHom X Ï† hÏ†) :=
  rfl

end Postcomposition

section Periodicity

variable {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]

/-! ### Summing translates of a function -/

/-- Summing the translates of `f` by `â„¤ â€¢ p` gives a map which is periodic with period `p`.
(This is true without any convergence conditions, since if the sum doesn't converge it is taken to
be the zero map, which is periodic.) -/
theorem periodic_tsum_comp_add_zsmul [AddCommGroup X] [TopologicalAddGroup X] [AddCommMonoid Y]
    [ContinuousAdd Y] [T2Space Y] (f : C(X, Y)) (p : X) :
    Function.Periodic (â‡‘(âˆ‘' n : â„¤, f.comp (ContinuousMap.addRight (n â€¢ p)))) p := by
  intro x
  by_cases h : Summable fun n : â„¤ => f.comp (ContinuousMap.addRight (n â€¢ p))
  Â· convert congr_arg (fun f : C(X, Y) => f x) ((Equiv.addRight (1 : â„¤)).tsum_eq _) using 1
    -- Porting note: in mathlib3 the proof from here was:
    -- simp_rw [â† tsum_apply h, â† tsum_apply ((equiv.add_right (1 : â„¤)).summable_iff.mpr h),
    --   equiv.coe_add_right, comp_apply, coe_add_right, add_one_zsmul, add_comm (_ â€¢ p) p,
    --   â† add_assoc]
    -- However now the second `â† tsum_apply` doesn't fire unless we use `erw`.
    simp_rw [â† tsum_apply h]
    erw [â† tsum_apply ((Equiv.addRight (1 : â„¤)).summable_iff.mpr h)]
    simp [coe_addRight, add_one_zsmul, add_comm (_ â€¢ p) p, â† add_assoc]
  Â· rw [tsum_eq_zero_of_not_summable h]
    simp only [coe_zero, Pi.zero_apply]
#align continuous_map.periodic_tsum_comp_add_zsmul ContinuousMap.periodic_tsum_comp_add_zsmul

end Periodicity

end ContinuousMap

namespace Homeomorph

variable {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
variable (ð•œ : Type*) [CommSemiring ð•œ]
variable (A : Type*) [TopologicalSpace A] [Semiring A] [TopologicalSemiring A] [StarRing A]
variable [ContinuousStar A] [Algebra ð•œ A]

/-- `ContinuousMap.compStarAlgHom'` as a `StarAlgEquiv` when the continuous map `f` is
actually a homeomorphism. -/
@[simps]
def compStarAlgEquiv' (f : X â‰ƒâ‚œ Y) : C(Y, A) â‰ƒâ‹†â‚[ð•œ] C(X, A) :=
  { f.toContinuousMap.compStarAlgHom' ð•œ A with
    toFun := (f : C(X, Y)).compStarAlgHom' ð•œ A
    invFun := (f.symm : C(Y, X)).compStarAlgHom' ð•œ A
    left_inv := fun g => by
      simp only [ContinuousMap.compStarAlgHom'_apply, ContinuousMap.comp_assoc,
        toContinuousMap_comp_symm, ContinuousMap.comp_id]
    right_inv := fun g => by
      simp only [ContinuousMap.compStarAlgHom'_apply, ContinuousMap.comp_assoc,
        symm_comp_toContinuousMap, ContinuousMap.comp_id]
    map_smul' := fun k a => map_smul (f.toContinuousMap.compStarAlgHom' ð•œ A) k a }
#align homeomorph.comp_star_alg_equiv' Homeomorph.compStarAlgEquiv'

end Homeomorph

/-! ### Evaluation as a bundled map -/

variable {X : Type*} (S R : Type*) [TopologicalSpace X] [CommSemiring S] [CommSemiring R]
variable [Algebra S R] [TopologicalSpace R] [TopologicalSemiring R]

/-- Evaluation of continuous maps at a point, bundled as an algebra homomorphism. -/
@[simps]
def ContinuousMap.evalAlgHom (x : X) : C(X, R) â†’â‚[S] R where
  toFun f := f x
  map_zero' := rfl
  map_one' := rfl
  map_add' _ _ := rfl
  map_mul' _ _ := rfl
  commutes' _ := rfl

/-- Evaluation of continuous maps at a point, bundled as a star algebra homomorphism. -/
@[simps!]
def ContinuousMap.evalStarAlgHom [StarRing R] [ContinuousStar R] (x : X) :
    C(X, R) â†’â‹†â‚[S] R :=
  { ContinuousMap.evalAlgHom S R x with
    map_star' := fun _ => rfl }
