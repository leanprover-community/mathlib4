/-
Copyright (c) 2024 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Algebra.Algebra.Quasispectrum
import Mathlib.Topology.ContinuousFunction.Compact
import Mathlib.Topology.ContinuousFunction.ContinuousMapZero
import Mathlib.Topology.ContinuousFunction.FunctionalCalculus
import Mathlib.Topology.UniformSpace.CompactConvergence

/-!
# The continuous functional calculus for non-unital algebras

This file defines a generic API for the *continuous functional calculus* in *non-unital* algebras
which is suitable in a wide range of settings. The design is intended to match as closely as
possible that for unital algebras in `Mathlib.Topology.ContinuousFunction.FunctionalCalculus`.
Changes to either file should be mirrored in its counterpart whenever possible. The underlying
reasons for the design decisions in the unital case apply equally in the non-unital case. See the
module documentation in that file for more information.

A continuous functional calculus for an element `a : A` in a non-unital topological `R`-algebra is
a continuous extension of the polynomial functional calculus (i.e., `Polynomial.aeval`) for
polynomials with no constant term to continuous `R`-valued functions on `quasispectrum R a` which
vanish at zero. More precisely, it is a continuous star algebra homomorphism
`C(quasispectrum R a, R)‚ÇÄ ‚Üí‚ãÜ‚Çô‚Çê[R] A` that sends `(ContinuousMap.id R).restrict (quasispectrum R a)`
to `a`. In all cases of interest (e.g., when `quasispectrum R a` is compact and `R` is `‚Ñù‚â•0`, `‚Ñù`,
or `‚ÑÇ`), this is sufficient to uniquely determine the continuous functional calculus which is
encoded in the `UniqueNonUnitalContinuousFunctionalCalculus` class.

## Main declarations

+ `NonUnitalContinuousFunctionalCalculus R (p : A ‚Üí Prop)`: a class stating that every `a : A`
  satisfying `p a` has a non-unital star algebra homomorphism from the continuous `R`-valued
  functions on the `R`-quasispectrum of `a` vanishing at zero into the algebra `A`. This map is a
  closed embedding, and satisfies the **spectral mapping theorem**.
+ `cfc‚ÇôHom : p a ‚Üí C(quasispectrum R a, R)‚ÇÄ ‚Üí‚ãÜ‚Çê[R] A`: the underlying non-unital star algebra
  homomorphism for an element satisfying property `p`.
+ `cfc‚Çô : (R ‚Üí R) ‚Üí A ‚Üí A`: an unbundled version of `cfc‚ÇôHom` which takes the junk value `0` when
  `cfc‚ÇôHom` is not defined.

## Main theorems

+ `cfc‚Çô_comp : cfc‚Çô (x ‚Ü¶ g (f x)) a = cfc‚Çô g (cfc‚Çô f a)`

-/
local notation "œÉ‚Çô" => quasispectrum

open scoped ContinuousMapZero

/-- A non-unital star `R`-algebra `A` has a *continuous functional calculus* for elements
satisfying the property `p : A ‚Üí Prop` if

+ for every such element `a : A` there is a non-unital star algebra homomorphism
  `cfc‚ÇôHom : C(quasispectrum R a, R)‚ÇÄ ‚Üí‚ãÜ‚Çô‚Çê[R] A` sending the (restriction of) the identity map
  to `a`.
+ `cfc‚ÇôHom` is a closed embedding for which the quasispectrum of the image of function `f` is its
  range.
+ `cfc‚ÇôHom` preserves the property `p`.

The property `p` is marked as an `outParam` so that the user need not specify it. In practice,

+ for `R := ‚ÑÇ`, we choose `p := IsStarNormal`,
+ for `R := ‚Ñù`, we choose `p := IsSelfAdjoint`,
+ for `R := ‚Ñù‚â•0`, we choose `p := (0 ‚â§ ¬∑)`.

Instead of directly providing the data we opt instead for a `Prop` class. In all relevant cases,
the continuous functional calculus is uniquely determined, and utilizing this approach
prevents diamonds or problems arising from multiple instances. -/
class NonUnitalContinuousFunctionalCalculus (R : Type*) {A : Type*} (p : outParam (A ‚Üí Prop))
    [CommSemiring R] [Nontrivial R] [StarRing R] [MetricSpace R] [TopologicalSemiring R]
    [ContinuousStar R] [NonUnitalRing A] [StarRing A] [TopologicalSpace A] [Module R A]
    [IsScalarTower R A A] [SMulCommClass R A A] : Prop where
  exists_cfc_of_predicate : ‚àÄ a, p a ‚Üí ‚àÉ œÜ : C(œÉ‚Çô R a, R)‚ÇÄ ‚Üí‚ãÜ‚Çô‚Çê[R] A,
    ClosedEmbedding œÜ ‚àß œÜ ‚ü®(ContinuousMap.id R).restrict <| œÉ‚Çô R a, rfl‚ü© = a ‚àß
      (‚àÄ f, œÉ‚Çô R (œÜ f) = Set.range f) ‚àß ‚àÄ f, p (œÜ f)

-- TODO: try to unify with the unital version. The `‚Ñù‚â•0` case makes it tricky.
/-- A class guaranteeing that the non-unital continuous functional calculus is uniquely determined
by the properties that it is a continuous non-unital star algebra homomorphism mapping the
(restriction of) the identity to `a`. This is the necessary tool used to establish `cfc‚ÇôHom_comp`
and the more common variant `cfc‚Çô_comp`.

This class will have instances in each of the common cases `‚ÑÇ`, `‚Ñù` and `‚Ñù‚â•0` as a consequence of
the Stone-Weierstrass theorem. -/
class UniqueNonUnitalContinuousFunctionalCalculus (R A : Type*) [CommSemiring R] [StarRing R]
    [MetricSpace R] [TopologicalSemiring R] [ContinuousStar R] [NonUnitalRing A] [StarRing A]
    [TopologicalSpace A] [Module R A] [IsScalarTower R A A] [SMulCommClass R A A] : Prop where
  eq_of_continuous_of_map_id (s : Set R) [CompactSpace s] [Zero s] (h0 : (0 : s) = (0 : R))
    (œÜ œà : C(s, R)‚ÇÄ ‚Üí‚ãÜ‚Çô‚Çê[R] A) (hœÜ : Continuous œÜ) (hœà : Continuous œà)
    (h : œÜ (‚ü®.restrict s <| .id R, h0‚ü©) = œà (‚ü®.restrict s <| .id R, h0‚ü©)) :
    œÜ = œà
  compactSpace_quasispectrum (a : A) : CompactSpace (œÉ‚Çô R a)

section Main

variable {R A : Type*} {p : A ‚Üí Prop} [CommSemiring R] [Nontrivial R] [StarRing R] [MetricSpace R]
variable [TopologicalSemiring R] [ContinuousStar R] [NonUnitalRing A] [StarRing A]
variable [TopologicalSpace A] [Module R A] [IsScalarTower R A A] [SMulCommClass R A A]
variable [NonUnitalContinuousFunctionalCalculus R p]

lemma NonUnitalStarAlgHom.ext_continuousMap [UniqueNonUnitalContinuousFunctionalCalculus R A]
    (a : A) (œÜ œà : C(œÉ‚Çô R a, R)‚ÇÄ ‚Üí‚ãÜ‚Çô‚Çê[R] A) (hœÜ : Continuous œÜ) (hœà : Continuous œà)
    (h : œÜ ‚ü®.restrict (œÉ‚Çô R a) <| .id R, rfl‚ü© = œà ‚ü®.restrict (œÉ‚Çô R a) <| .id R, rfl‚ü©) :
    œÜ = œà :=
  have := UniqueNonUnitalContinuousFunctionalCalculus.compactSpace_quasispectrum (R := R) a
  UniqueNonUnitalContinuousFunctionalCalculus.eq_of_continuous_of_map_id _ (by simp) œÜ œà hœÜ hœà h

section cfc‚ÇôHom

variable {a : A} (ha : p a)

/-- The non-unital star algebra homomorphism underlying a instance of the continuous functional
calculus for non-unital algebras; a version for continuous functions on the quasispectrum.

In this case, the user must supply the fact that `a` satisfies the predicate `p`.

While `NonUnitalContinuousFunctionalCalculus` is stated in terms of these homomorphisms, in practice
the user should instead prefer `cfc‚Çô` over `cfc‚ÇôHom`.
-/
noncomputable def cfc‚ÇôHom : C(œÉ‚Çô R a, R)‚ÇÄ ‚Üí‚ãÜ‚Çô‚Çê[R] A :=
  (NonUnitalContinuousFunctionalCalculus.exists_cfc_of_predicate a ha).choose

lemma cfc‚ÇôHom_closedEmbedding :
    ClosedEmbedding <| (cfc‚ÇôHom ha : C(œÉ‚Çô R a, R)‚ÇÄ ‚Üí‚ãÜ‚Çô‚Çê[R] A) :=
  (NonUnitalContinuousFunctionalCalculus.exists_cfc_of_predicate a ha).choose_spec.1

lemma cfc‚ÇôHom_id :
    cfc‚ÇôHom ha ‚ü®(ContinuousMap.id R).restrict <| œÉ‚Çô R a, rfl‚ü© = a :=
  (NonUnitalContinuousFunctionalCalculus.exists_cfc_of_predicate a ha).choose_spec.2.1

/-- The **spectral mapping theorem** for the non-unital continuous functional calculus. -/
lemma cfc‚ÇôHom_map_quasispectrum (f : C(œÉ‚Çô R a, R)‚ÇÄ) :
    œÉ‚Çô R (cfc‚ÇôHom ha f) = Set.range f :=
  (NonUnitalContinuousFunctionalCalculus.exists_cfc_of_predicate a ha).choose_spec.2.2.1 f

lemma cfc‚ÇôHom_predicate (f : C(œÉ‚Çô R a, R)‚ÇÄ) :
    p (cfc‚ÇôHom ha f) :=
  (NonUnitalContinuousFunctionalCalculus.exists_cfc_of_predicate a ha).choose_spec.2.2.2 f

lemma cfc‚ÇôHom_eq_of_continuous_of_map_id [UniqueNonUnitalContinuousFunctionalCalculus R A]
    (œÜ : C(œÉ‚Çô R a, R)‚ÇÄ ‚Üí‚ãÜ‚Çô‚Çê[R] A) (hœÜ‚ÇÅ : Continuous œÜ)
    (hœÜ‚ÇÇ : œÜ ‚ü®.restrict (œÉ‚Çô R a) <| .id R, rfl‚ü© = a) : cfc‚ÇôHom ha = œÜ :=
  (cfc‚ÇôHom ha).ext_continuousMap a œÜ (cfc‚ÇôHom_closedEmbedding ha).continuous hœÜ‚ÇÅ <| by
    rw [cfc‚ÇôHom_id ha, hœÜ‚ÇÇ]

theorem cfc‚ÇôHom_comp [UniqueNonUnitalContinuousFunctionalCalculus R A] (f : C(œÉ‚Çô R a, R)‚ÇÄ)
    (f' : C(œÉ‚Çô R a, œÉ‚Çô R (cfc‚ÇôHom ha f))‚ÇÄ)
    (hff' : ‚àÄ x, f x = f' x) (g : C(œÉ‚Çô R (cfc‚ÇôHom ha f), R)‚ÇÄ) :
    cfc‚ÇôHom ha (g.comp f') = cfc‚ÇôHom (cfc‚ÇôHom_predicate ha f) g := by
  let œà : C(œÉ‚Çô R (cfc‚ÇôHom ha f), R)‚ÇÄ ‚Üí‚ãÜ‚Çô‚Çê[R] C(œÉ‚Çô R a, R)‚ÇÄ :=
    { toFun := (ContinuousMapZero.comp ¬∑ f')
      map_smul' := fun _ _ ‚Ü¶ rfl
      map_add' := fun _ _ ‚Ü¶ rfl
      map_mul' := fun _ _ ‚Ü¶ rfl
      map_zero' := rfl
      map_star' := fun _ ‚Ü¶ rfl }
  let œÜ : C(œÉ‚Çô R (cfc‚ÇôHom ha f), R)‚ÇÄ ‚Üí‚ãÜ‚Çô‚Çê[R] A := (cfc‚ÇôHom ha).comp œà
  suffices cfc‚ÇôHom (cfc‚ÇôHom_predicate ha f) = œÜ from DFunLike.congr_fun this.symm g
  refine cfc‚ÇôHom_eq_of_continuous_of_map_id (cfc‚ÇôHom_predicate ha f) œÜ ?_ ?_
  ¬∑ refine (cfc‚ÇôHom_closedEmbedding ha).continuous.comp <| continuous_induced_rng.mpr ?_
    exact f'.toContinuousMap.continuous_comp_left.comp continuous_induced_dom
  ¬∑ simp only [œÜ, œà, NonUnitalStarAlgHom.comp_apply, NonUnitalStarAlgHom.coe_mk',
      NonUnitalAlgHom.coe_mk]
    congr
    ext x
    simp [hff']

end cfc‚ÇôHom


section CFCn

open Classical in
/-- This is the *continuous functional calculus* of an element `a : A` in a non-unital algebra
applied to bare functions.  When either `a` does not satisfy the predicate `p` (i.e., `a` is not
`IsStarNormal`, `IsSelfAdjoint`, or `0 ‚â§ a` when `R` is `‚ÑÇ`, `‚Ñù`, or `‚Ñù‚â•0`, respectively), or when
`f : R ‚Üí R` is not continuous on the quasispectrum of `a` or `f 0 ‚â† 0`, then `cfc‚Çô f a` returns the
junk value `0`.

This is the primary declaration intended for widespread use of the continuous functional calculus
for non-unital algebras, and all the API applies to this declaration. For more information, see the
module documentation for `Topology.ContinuousFunction.FunctionalCalculus`. -/
noncomputable irreducible_def cfc‚Çô (f : R ‚Üí R) (a : A) : A :=
  if h : p a ‚àß ContinuousOn f (œÉ‚Çô R a) ‚àß f 0 = 0
    then cfc‚ÇôHom h.1 ‚ü®‚ü®_, h.2.1.restrict‚ü©, h.2.2‚ü©
    else 0

/-- A tactic used to automatically discharge goals relating to the continuous functional calculus,
specifically concerning whether `f 0 = 0`. -/
syntax (name := cfcZeroTac) "cfc_zero_tac" : tactic
macro_rules
  | `(tactic| cfc_zero_tac) => `(tactic| try (first | aesop | assumption))

variable (f g : R ‚Üí R) (a : A)
variable (hf : ContinuousOn f (œÉ‚Çô R a) := by cfc_cont_tac) (hf0 : f 0 = 0 := by cfc_zero_tac)
variable (hg : ContinuousOn g (œÉ‚Çô R a) := by cfc_cont_tac) (hg0 : g 0 = 0 := by cfc_zero_tac)
variable (ha : p a := by cfc_tac)

lemma cfc‚Çô_apply : cfc‚Çô f a = cfc‚ÇôHom (a := a) ha ‚ü®‚ü®_, hf.restrict‚ü©, hf0‚ü© := by
  rw [cfc‚Çô_def, dif_pos ‚ü®ha, hf, hf0‚ü©]

lemma cfc‚Çô_apply_of_not_and_and {f : R ‚Üí R} (a : A)
    (ha : ¬¨ (p a ‚àß ContinuousOn f (œÉ‚Çô R a) ‚àß f 0 = 0)) :
    cfc‚Çô f a = 0 := by
  rw [cfc‚Çô_def, dif_neg ha]

lemma cfc‚Çô_apply_of_not_predicate {f : R ‚Üí R} (a : A) (ha : ¬¨ p a) :
    cfc‚Çô f a = 0 := by
  rw [cfc‚Çô_def, dif_neg (not_and_of_not_left _ ha)]

lemma cfc‚Çô_apply_of_not_continuousOn {f : R ‚Üí R} (a : A) (hf : ¬¨ ContinuousOn f (œÉ‚Çô R a)) :
    cfc‚Çô f a = 0 := by
  rw [cfc‚Çô_def, dif_neg (not_and_of_not_right _ (not_and_of_not_left _ hf))]

lemma cfc‚Çô_apply_of_not_map_zero {f : R ‚Üí R} (a : A) (hf : ¬¨ f 0 = 0) :
    cfc‚Çô f a = 0 := by
  rw [cfc‚Çô_def, dif_neg (not_and_of_not_right _ (not_and_of_not_right _ hf))]

lemma cfc‚ÇôHom_eq_cfc‚Çô_extend {a : A} (g : R ‚Üí R) (ha : p a) (f : C(œÉ‚Çô R a, R)‚ÇÄ) :
    cfc‚ÇôHom ha f = cfc‚Çô (Function.extend Subtype.val f g) a := by
  have h : f = (œÉ‚Çô R a).restrict (Function.extend Subtype.val f g) := by
    ext; simp [Subtype.val_injective.extend_apply]
  have hg : ContinuousOn (Function.extend Subtype.val f g) (œÉ‚Çô R a) :=
    continuousOn_iff_continuous_restrict.mpr <| h ‚ñ∏ map_continuous f
  have hg0 : (Function.extend Subtype.val f g) 0 = 0 := by
    rw [‚Üê quasispectrum.coe_zero (R := R) a, Subtype.val_injective.extend_apply]
    exact map_zero f
  rw [cfc‚Çô_apply ..]
  congr!

lemma cfc‚Çô_cases (P : A ‚Üí Prop) (a : A) (f : R ‚Üí R) (h‚ÇÄ : P 0)
    (haf : ‚àÄ (hf : ContinuousOn f (œÉ‚Çô R a)) h0 ha, P (cfc‚ÇôHom ha ‚ü®‚ü®_, hf.restrict‚ü©, h0‚ü©)) :
    P (cfc‚Çô f a) := by
  by_cases h : ContinuousOn f (œÉ‚Çô R a) ‚àß f 0 = 0 ‚àß p a
  ¬∑ rw [cfc‚Çô_apply f a h.1 h.2.1 h.2.2]
    exact haf h.1 h.2.1 h.2.2
  ¬∑ simp only [not_and_or] at h
    obtain (h | h | h) := h
    ¬∑ rwa [cfc‚Çô_apply_of_not_continuousOn _ h]
    ¬∑ rwa [cfc‚Çô_apply_of_not_map_zero _ h]
    ¬∑ rwa [cfc‚Çô_apply_of_not_predicate _ h]

variable (R) in
lemma cfc‚Çô_id : cfc‚Çô (id : R ‚Üí R) a = a :=
  cfc‚Çô_apply (id : R ‚Üí R) a ‚ñ∏ cfc‚ÇôHom_id (p := p) ha

variable (R) in
lemma cfc‚Çô_id' : cfc‚Çô (fun x : R ‚Ü¶ x) a = a := cfc‚Çô_id R a

/-- The **spectral mapping theorem** for the non-unital continuous functional calculus. -/
lemma cfc‚Çô_map_quasispectrum : œÉ‚Çô R (cfc‚Çô f a) = f '' œÉ‚Çô R a := by
  simp [cfc‚Çô_apply f a, cfc‚ÇôHom_map_quasispectrum (p := p)]

lemma cfc‚Çô_predicate : p (cfc‚Çô f a) :=
  cfc‚Çô_apply f a ‚ñ∏ cfc‚ÇôHom_predicate (A := A) ha _

lemma cfc‚Çô_congr {f g : R ‚Üí R} {a : A} (hfg : (œÉ‚Çô R a).EqOn f g) :
    cfc‚Çô f a = cfc‚Çô g a := by
  by_cases h : p a ‚àß ContinuousOn g (œÉ‚Çô R a) ‚àß g 0 = 0
  ¬∑ rw [cfc‚Çô_apply f a (h.2.1.congr hfg) (hfg (quasispectrum.zero_mem R a) ‚ñ∏ h.2.2) h.1,
      cfc‚Çô_apply g a h.2.1 h.2.2 h.1]
    congr
    exact Set.restrict_eq_iff.mpr hfg
  ¬∑ simp only [not_and_or] at h
    obtain (ha | hg | h0) := h
    ¬∑ simp [cfc‚Çô_apply_of_not_predicate a ha]
    ¬∑ rw [cfc‚Çô_apply_of_not_continuousOn a hg, cfc‚Çô_apply_of_not_continuousOn]
      exact fun hf ‚Ü¶ hg (hf.congr hfg.symm)
    ¬∑ rw [cfc‚Çô_apply_of_not_map_zero a h0, cfc‚Çô_apply_of_not_map_zero]
      exact fun hf ‚Ü¶ h0 (hfg (quasispectrum.zero_mem R a) ‚ñ∏ hf)

lemma eqOn_of_cfc‚Çô_eq_cfc‚Çô {f g : R ‚Üí R} {a : A} (h : cfc‚Çô f a = cfc‚Çô g a) (ha : p a := by cfc_tac)
    (hf : ContinuousOn f (œÉ‚Çô R a) := by cfc_cont_tac) (hf0 : f 0 = 0 := by cfc_zero_tac)
    (hg : ContinuousOn g (œÉ‚Çô R a) := by cfc_cont_tac) (hg0 : g 0 = 0 := by cfc_zero_tac) :
    (œÉ‚Çô R a).EqOn f g := by
  rw [cfc‚Çô_apply f a, cfc‚Çô_apply g a] at h
  have := (cfc‚ÇôHom_closedEmbedding (show p a from ha) (R := R)).inj h
  intro x hx
  congrm($(this) ‚ü®x, hx‚ü©)

lemma cfc‚Çô_eq_cfc‚Çô_iff_eqOn {f g : R ‚Üí R} {a : A} (ha : p a := by cfc_tac)
    (hf : ContinuousOn f (œÉ‚Çô R a) := by cfc_cont_tac) (hf0 : f 0 = 0 := by cfc_zero_tac)
    (hg : ContinuousOn g (œÉ‚Çô R a) := by cfc_cont_tac) (hg0 : g 0 = 0 := by cfc_zero_tac) :
    cfc‚Çô f a = cfc‚Çô g a ‚Üî (œÉ‚Çô R a).EqOn f g :=
  ‚ü®eqOn_of_cfc‚Çô_eq_cfc‚Çô, cfc‚Çô_congr‚ü©

variable (R)

@[simp]
lemma cfc‚Çô_zero : cfc‚Çô (0 : R ‚Üí R) a = 0 := by
  by_cases ha : p a
  ¬∑ exact cfc‚Çô_apply (0 : R ‚Üí R) a ‚ñ∏ map_zero (cfc‚ÇôHom ha)
  ¬∑ rw [cfc‚Çô_apply_of_not_predicate a ha]

@[simp]
lemma cfc‚Çô_const_zero : cfc‚Çô (fun _ : R ‚Ü¶ 0) a = 0 := cfc‚Çô_zero R a

variable {R}

lemma cfc‚Çô_mul : cfc‚Çô (fun x ‚Ü¶ f x * g x) a = cfc‚Çô f a * cfc‚Çô g a := by
  by_cases ha : p a
  ¬∑ rw [cfc‚Çô_apply f a, cfc‚Çô_apply g a, ‚Üê map_mul, cfc‚Çô_apply _ a]
    congr
  ¬∑ simp [cfc‚Çô_apply_of_not_predicate a ha]

lemma cfc‚Çô_add : cfc‚Çô (fun x ‚Ü¶ f x + g x) a = cfc‚Çô f a + cfc‚Çô g a := by
  by_cases ha : p a
  ¬∑ rw [cfc‚Çô_apply f a, cfc‚Çô_apply g a, cfc‚Çô_apply _ a]
    simp_rw [‚Üê map_add]
    congr
  ¬∑ simp [cfc‚Çô_apply_of_not_predicate a ha]

lemma cfc‚Çô_smul {S : Type*} [SMulZeroClass S R] [ContinuousConstSMul S R]
    [SMulZeroClass S A] [IsScalarTower S R A] [IsScalarTower S R (R ‚Üí R)]
    (s : S) (f : R ‚Üí R) (a : A) (hf : ContinuousOn f (œÉ‚Çô R a) := by cfc_cont_tac)
    (h0 : f 0 = 0 := by cfc_zero_tac) :
    cfc‚Çô (fun x ‚Ü¶ s ‚Ä¢ f x) a = s ‚Ä¢ cfc‚Çô f a := by
  by_cases ha : p a
  ¬∑ rw [cfc‚Çô_apply f a, cfc‚Çô_apply _ a]
    simp_rw [‚Üê Pi.smul_def, ‚Üê smul_one_smul R s _]
    rw [‚Üê map_smul]
    congr
  ¬∑ simp [cfc‚Çô_apply_of_not_predicate a ha]

lemma cfc‚Çô_const_mul (r : R) (f : R ‚Üí R) (a : A) (hf : ContinuousOn f (œÉ‚Çô R a) := by cfc_cont_tac)
    (h0 : f 0 = 0 := by cfc_zero_tac) :
    cfc‚Çô (fun x ‚Ü¶ r * f x) a = r ‚Ä¢ cfc‚Çô f a :=
  cfc‚Çô_smul r f a

lemma cfc‚Çô_star : cfc‚Çô (fun x ‚Ü¶ star (f x)) a = star (cfc‚Çô f a) := by
  by_cases h : p a ‚àß ContinuousOn f (œÉ‚Çô R a) ‚àß f 0 = 0
  ¬∑ obtain ‚ü®ha, hf, h0‚ü© := h
    rw [cfc‚Çô_apply f a, ‚Üê map_star, cfc‚Çô_apply _ a]
    congr
  ¬∑ simp only [not_and_or] at h
    obtain (ha | hf | h0) := h
    ¬∑ simp [cfc‚Çô_apply_of_not_predicate a ha]
    ¬∑ rw [cfc‚Çô_apply_of_not_continuousOn a hf, cfc‚Çô_apply_of_not_continuousOn, star_zero]
      exact fun hf_star ‚Ü¶ hf <| by simpa using hf_star.star
    ¬∑ rw [cfc‚Çô_apply_of_not_map_zero a h0, cfc‚Çô_apply_of_not_map_zero, star_zero]
      exact fun hf0 ‚Ü¶ h0 <| by simpa using congr(star $(hf0))

lemma cfc‚Çô_smul_id {S : Type*} [SMulZeroClass S R] [ContinuousConstSMul S R]
    [SMulZeroClass S A] [IsScalarTower S R A] [IsScalarTower S R (R ‚Üí R)]
    (s : S) (a : A) (ha : p a := by cfc_tac) : cfc‚Çô (s ‚Ä¢ ¬∑ : R ‚Üí R) a = s ‚Ä¢ a := by
  rw [cfc‚Çô_smul s _ a, cfc‚Çô_id' R a]

lemma cfc‚Çô_const_mul_id (r : R) (a : A) (ha : p a := by cfc_tac) : cfc‚Çô (r * ¬∑) a = r ‚Ä¢ a :=
  cfc‚Çô_smul_id r a

lemma cfc‚Çô_star_id : cfc‚Çô (star ¬∑ : R ‚Üí R) a = star a := by
  rw [cfc‚Çô_star _ a, cfc‚Çô_id' R a]

variable [UniqueNonUnitalContinuousFunctionalCalculus R A]

lemma cfc‚Çô_comp (g f : R ‚Üí R) (a : A)
    (hg : ContinuousOn g (f '' œÉ‚Çô R a) := by cfc_cont_tac) (hg0 : g 0 = 0 := by cfc_zero_tac)
    (hf : ContinuousOn f (œÉ‚Çô R a) := by cfc_cont_tac) (hf0 : f 0 = 0 := by cfc_zero_tac)
    (ha : p a := by cfc_tac) :
    cfc‚Çô (g ‚àò f) a = cfc‚Çô g (cfc‚Çô f a) := by
  have := hg.comp hf <| (œÉ‚Çô R a).mapsTo_image f
  have sp_eq :
      œÉ‚Çô R (cfc‚ÇôHom (show p a from ha) ‚ü®ContinuousMap.mk _ hf.restrict, hf0‚ü©) = f '' (œÉ‚Çô R a) := by
    rw [cfc‚ÇôHom_map_quasispectrum (by exact ha) _]
    ext
    simp
  rw [cfc‚Çô_apply .., cfc‚Çô_apply f a,
    cfc‚Çô_apply _ (by convert hg) (ha := cfc‚ÇôHom_predicate (show p a from ha) _) ,
    ‚Üê cfc‚ÇôHom_comp _ _]
  swap
  ¬∑ exact ‚ü®.mk _ <| hf.restrict.codRestrict fun x ‚Ü¶ by rw [sp_eq]; use x.1; simp, Subtype.ext hf0‚ü©
  ¬∑ congr
  ¬∑ exact fun _ ‚Ü¶ rfl

lemma cfc‚Çô_comp' (g f : R ‚Üí R) (a : A)
    (hg : ContinuousOn g (f '' œÉ‚Çô R a) := by cfc_cont_tac) (hg0 : g 0 = 0 := by cfc_zero_tac)
    (hf : ContinuousOn f (œÉ‚Çô R a) := by cfc_cont_tac) (hf0 : f 0 = 0 := by cfc_zero_tac)
    (ha : p a := by cfc_tac) :
    cfc‚Çô (g <| f ¬∑) a = cfc‚Çô g (cfc‚Çô f a) :=
  cfc‚Çô_comp g f a

lemma cfc‚Çô_comp_smul {S : Type*} [SMulZeroClass S R] [ContinuousConstSMul S R]
    [SMulZeroClass S A] [IsScalarTower S R A] [IsScalarTower S R (R ‚Üí R)]
    (s : S) (f : R ‚Üí R) (a : A) (hf : ContinuousOn f ((s ‚Ä¢ ¬∑) '' (œÉ‚Çô R a)) := by cfc_cont_tac)
    (hf0 : f 0 = 0 := by cfc_zero_tac) (ha : p a := by cfc_tac) :
    cfc‚Çô (f <| s ‚Ä¢ ¬∑) a = cfc‚Çô f (s ‚Ä¢ a) := by
  rw [cfc‚Çô_comp' f (s ‚Ä¢ ¬∑) a, cfc‚Çô_smul_id s a]

lemma cfc‚Çô_comp_const_mul (r : R) (f : R ‚Üí R) (a : A)
    (hf : ContinuousOn f ((r * ¬∑) '' (œÉ‚Çô R a)) := by cfc_cont_tac)
    (hf0 : f 0 = 0 := by cfc_zero_tac) (ha : p a := by cfc_tac) :
    cfc‚Çô (f <| r * ¬∑) a = cfc‚Çô f (r ‚Ä¢ a) := by
  rw [cfc‚Çô_comp' f (r * ¬∑) a, cfc‚Çô_const_mul_id r a]

lemma cfc‚Çô_comp_star (hf : ContinuousOn f (star '' (œÉ‚Çô R a)) := by cfc_cont_tac)
    (hf0 : f 0 = 0 := by cfc_zero_tac) (ha : p a := by cfc_tac) :
    cfc‚Çô (f <| star ¬∑) a = cfc‚Çô f (star a) := by
  rw [cfc‚Çô_comp' f star a, cfc‚Çô_star_id a]

lemma eq_zero_of_quasispectrum_eq_zero (h_spec : œÉ‚Çô R a ‚äÜ {0}) (ha : p a := by cfc_tac) :
    a = 0 := by
  simpa [cfc‚Çô_id R a] using cfc‚Çô_congr (a := a) (f := id) (g := fun _ : R ‚Ü¶ 0) fun x ‚Ü¶ by simp_all

end CFCn

end Main

section Neg

variable {R A : Type*} {p : A ‚Üí Prop} [CommRing R] [Nontrivial R] [StarRing R] [MetricSpace R]
variable [TopologicalRing R] [ContinuousStar R] [TopologicalSpace A] [NonUnitalRing A] [StarRing A]
variable [Module R A] [IsScalarTower R A A] [SMulCommClass R A A]
variable [NonUnitalContinuousFunctionalCalculus R p]
variable (f g : R ‚Üí R) (a : A)
variable (hf : ContinuousOn f (œÉ‚Çô R a) := by cfc_cont_tac) (hf0 : f 0 = 0 := by cfc_zero_tac)
variable (hg : ContinuousOn g (œÉ‚Çô R a) := by cfc_cont_tac) (hg0 : g 0 = 0 := by cfc_zero_tac)

lemma cfc‚Çô_sub : cfc‚Çô (fun x ‚Ü¶ f x - g x) a = cfc‚Çô f a - cfc‚Çô g a := by
  by_cases ha : p a
  ¬∑ rw [cfc‚Çô_apply f a, cfc‚Çô_apply g a, ‚Üê map_sub, cfc‚Çô_apply ..]
    congr
  ¬∑ simp [cfc‚Çô_apply_of_not_predicate a ha]

lemma cfc‚Çô_neg : cfc‚Çô (fun x ‚Ü¶ - (f x)) a = - (cfc‚Çô f a) := by
  by_cases h : p a ‚àß ContinuousOn f (œÉ‚Çô R a) ‚àß f 0 = 0
  ¬∑ obtain ‚ü®ha, hf, h0‚ü© := h
    rw [cfc‚Çô_apply f a, ‚Üê map_neg, cfc‚Çô_apply ..]
    congr
  ¬∑ simp only [not_and_or] at h
    obtain (ha | hf | h0) := h
    ¬∑ simp [cfc‚Çô_apply_of_not_predicate a ha]
    ¬∑ rw [cfc‚Çô_apply_of_not_continuousOn a hf, cfc‚Çô_apply_of_not_continuousOn, neg_zero]
      exact fun hf_neg ‚Ü¶ hf <| by simpa using hf_neg.neg
    ¬∑ rw [cfc‚Çô_apply_of_not_map_zero a h0, cfc‚Çô_apply_of_not_map_zero, neg_zero]
      exact (h0 <| neg_eq_zero.mp ¬∑)

lemma cfc‚Çô_neg_id (ha : p a := by cfc_tac) :
    cfc‚Çô (- ¬∑ : R ‚Üí R) a = -a := by
  rw [cfc‚Çô_neg .., cfc‚Çô_id' R a]

variable [UniqueNonUnitalContinuousFunctionalCalculus R A]

lemma cfc‚Çô_comp_neg (hf : ContinuousOn f ((- ¬∑) '' (œÉ‚Çô R a)) := by cfc_cont_tac)
    (h0 : f 0 = 0 := by cfc_zero_tac) (ha : p a := by cfc_tac) :
    cfc‚Çô (f <| - ¬∑) a = cfc‚Çô f (-a) := by
  rw [cfc‚Çô_comp' .., cfc‚Çô_neg_id _]

end Neg

section Order

section Semiring

variable {R A : Type*} {p : A ‚Üí Prop} [OrderedCommSemiring R] [Nontrivial R]
variable [StarRing R] [StarOrderedRing R] [MetricSpace R] [TopologicalSemiring R] [ContinuousStar R]
variable [‚àÄ (Œ±) [Zero Œ±] [TopologicalSpace Œ±], StarOrderedRing C(Œ±, R)‚ÇÄ]
variable [TopologicalSpace A] [NonUnitalRing A] [StarRing A] [PartialOrder A] [StarOrderedRing A]
variable [Module R A] [IsScalarTower R A A] [SMulCommClass R A A] [StarModule R A]
variable [NonUnitalContinuousFunctionalCalculus R p] [NonnegSpectrumClass R A]

lemma cfc‚ÇôHom_mono {a : A} (ha : p a) {f g : C(œÉ‚Çô R a, R)‚ÇÄ} (hfg : f ‚â§ g) :
    cfc‚ÇôHom ha f ‚â§ cfc‚ÇôHom ha g :=
  OrderHomClass.mono (cfc‚ÇôHom ha) hfg

lemma cfc‚ÇôHom_nonneg_iff {a : A} (ha : p a) {f : C(œÉ‚Çô R a, R)‚ÇÄ} :
    0 ‚â§ cfc‚ÇôHom ha f ‚Üî 0 ‚â§ f := by
  constructor
  ¬∑ exact fun hf x ‚Ü¶
      (cfc‚ÇôHom_map_quasispectrum ha (R := R) _ ‚ñ∏ quasispectrum_nonneg_of_nonneg (cfc‚ÇôHom ha f) hf)
      _ ‚ü®x, rfl‚ü©
  ¬∑ simpa using (cfc‚ÇôHom_mono ha (f := 0) (g := f) ¬∑)

lemma cfc‚Çô_mono {f g : R ‚Üí R} {a : A} (h : ‚àÄ x ‚àà œÉ‚Çô R a, f x ‚â§ g x)
    (hf : ContinuousOn f (œÉ‚Çô R a) := by cfc_cont_tac)
    (hg : ContinuousOn g (œÉ‚Çô R a) := by cfc_cont_tac)
    (hf0 : f 0 = 0 := by cfc_zero_tac) (hg0 : g 0 = 0 := by cfc_zero_tac) :
    cfc‚Çô f a ‚â§ cfc‚Çô g a := by
  by_cases ha : p a
  ¬∑ rw [cfc‚Çô_apply f a, cfc‚Çô_apply g a]
    exact cfc‚ÇôHom_mono ha fun x ‚Ü¶ h x.1 x.2
  ¬∑ simp only [cfc‚Çô_apply_of_not_predicate _ ha, le_rfl]

lemma cfc‚Çô_nonneg_iff (f : R ‚Üí R) (a : A) (hf : ContinuousOn f (œÉ‚Çô R a) := by cfc_cont_tac)
    (h0 : f 0 = 0 := by cfc_zero_tac) (ha : p a := by cfc_tac) :
    0 ‚â§ cfc‚Çô f a ‚Üî ‚àÄ x ‚àà œÉ‚Çô R a, 0 ‚â§ f x := by
  rw [cfc‚Çô_apply .., cfc‚ÇôHom_nonneg_iff, ContinuousMapZero.le_def]
  simp only [ContinuousMapZero.coe_mk, ContinuousMap.coe_mk, Set.restrict_apply, Subtype.forall]
  congr!

lemma cfc‚Çô_nonneg {f : R ‚Üí R} {a : A} (h : ‚àÄ x ‚àà œÉ‚Çô R a, 0 ‚â§ f x) :
    0 ‚â§ cfc‚Çô f a := by
  by_cases hf : ContinuousOn f (œÉ‚Çô R a) ‚àß f 0 = 0
  ¬∑ obtain ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© := hf
    simpa using cfc‚Çô_mono h
  ¬∑ simp only [not_and_or] at hf
    obtain (hf | hf) := hf
    ¬∑ simp only [cfc‚Çô_apply_of_not_continuousOn _ hf, le_rfl]
    ¬∑ simp only [cfc‚Çô_apply_of_not_map_zero _ hf, le_rfl]

lemma cfc‚Çô_nonpos (f : R ‚Üí R) (a : A) (h : ‚àÄ x ‚àà œÉ‚Çô R a, f x ‚â§ 0) :
    cfc‚Çô f a ‚â§ 0 := by
  by_cases hf : ContinuousOn f (œÉ‚Çô R a) ‚àß f 0 = 0
  ¬∑ obtain ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© := hf
    simpa using cfc‚Çô_mono h
  ¬∑ simp only [not_and_or] at hf
    obtain (hf | hf) := hf
    ¬∑ simp only [cfc‚Çô_apply_of_not_continuousOn _ hf, le_rfl]
    ¬∑ simp only [cfc‚Çô_apply_of_not_map_zero _ hf, le_rfl]

end Semiring

section Ring

variable {R A : Type*} {p : A ‚Üí Prop} [OrderedCommRing R] [Nontrivial R]
variable [StarRing R] [StarOrderedRing R] [MetricSpace R] [TopologicalRing R] [ContinuousStar R]
variable [‚àÄ (Œ±) [Zero Œ±] [TopologicalSpace Œ±], StarOrderedRing C(Œ±, R)‚ÇÄ]
variable [TopologicalSpace A] [NonUnitalRing A] [StarRing A] [PartialOrder A] [StarOrderedRing A]
variable [Module R A] [IsScalarTower R A A] [SMulCommClass R A A] [StarModule R A]
variable [NonUnitalContinuousFunctionalCalculus R p] [NonnegSpectrumClass R A]

lemma cfc‚ÇôHom_le_iff {a : A} (ha : p a) {f g : C(œÉ‚Çô R a, R)‚ÇÄ} :
    cfc‚ÇôHom ha f ‚â§ cfc‚ÇôHom ha g ‚Üî f ‚â§ g := by
  rw [‚Üê sub_nonneg, ‚Üê map_sub, cfc‚ÇôHom_nonneg_iff, sub_nonneg]

lemma cfc‚Çô_le_iff (f g : R ‚Üí R) (a : A) (hf : ContinuousOn f (œÉ‚Çô R a) := by cfc_cont_tac)
    (hg : ContinuousOn g (œÉ‚Çô R a) := by cfc_cont_tac) (hf0 : f 0 = 0 := by cfc_zero_tac)
    (hg0 : g 0 = 0 := by cfc_zero_tac) (ha : p a := by cfc_tac) :
    cfc‚Çô f a ‚â§ cfc‚Çô g a ‚Üî ‚àÄ x ‚àà œÉ‚Çô R a, f x ‚â§ g x := by
  rw [cfc‚Çô_apply f a, cfc‚Çô_apply g a, cfc‚ÇôHom_le_iff (show p a from ha), ContinuousMapZero.le_def]
  simp

lemma cfc‚Çô_nonpos_iff (f : R ‚Üí R) (a : A) (hf : ContinuousOn f (œÉ‚Çô R a) := by cfc_cont_tac)
    (h0 : f 0 = 0 := by cfc_zero_tac) (ha : p a := by cfc_tac) :
    cfc‚Çô f a ‚â§ 0 ‚Üî ‚àÄ x ‚àà œÉ‚Çô R a, f x ‚â§ 0 := by
  simp_rw [‚Üê neg_nonneg, ‚Üê cfc‚Çô_neg]
  exact cfc‚Çô_nonneg_iff (fun x ‚Ü¶ -f x) a

end Ring

end Order

/-! ### Obtain a non-unital continuous functional calculus from a unital one -/

section UnitalToNonUnital

open ContinuousMapZero Set Uniformity ContinuousMap

variable {R A : Type*} {p : A ‚Üí Prop} [Field R] [StarRing R] [MetricSpace R] [CompleteSpace R]
variable [TopologicalRing R] [ContinuousStar R] [Ring A] [StarRing A] [TopologicalSpace A]
variable [Algebra R A] [ContinuousFunctionalCalculus R p]
variable [h_cpct : ‚àÄ a : A, CompactSpace (spectrum R a)]

instance ContinuousFunctionalCalculus.toNonUnital : NonUnitalContinuousFunctionalCalculus R p where
  exists_cfc_of_predicate a ha := by
    have h_cpct' : CompactSpace (quasispectrum R a) := by
      specialize h_cpct a
      simp_rw [‚Üê isCompact_iff_compactSpace, quasispectrum_eq_spectrum_union_zero] at h_cpct ‚ä¢
      exact h_cpct.union isCompact_singleton
    let e := ContinuousMapZero.toContinuousMapHom (X := quasispectrum R a) (R := R)
    let f : C(spectrum R a, quasispectrum R a) :=
      ‚ü®_, continuous_inclusion <| spectrum_subset_quasispectrum R a‚ü©
    let œà := ContinuousMap.compStarAlgHom' R R f
    let œà' := (cfcHom ha (R := R) : C(spectrum R a, R) ‚Üí‚ãÜ‚Çô‚Çê[R] A).comp <|
      (œà : C(quasispectrum R a, R) ‚Üí‚ãÜ‚Çô‚Çê[R] C(spectrum R a, R)).comp e
    refine ‚ü®œà', ?closedEmbedding, ?map_id, ?map_spectrum, ?predicate‚ü©
    case closedEmbedding =>
      refine (cfcHom_closedEmbedding ha).comp <|
        (UniformInducing.uniformEmbedding ‚ü®?_‚ü©).toClosedEmbedding
      have := uniformSpace_eq_inf_precomp_of_cover (Œ≤ := R) f (0 : C(Unit, œÉ‚Çô R a))
        (map_continuous f).isProperMap (map_continuous 0).isProperMap <| by
          simp only [‚Üê Subtype.val_injective.image_injective.eq_iff, f, ContinuousMap.coe_mk,
            ContinuousMap.coe_zero, range_zero, image_union, image_singleton,
            quasispectrum.coe_zero, ‚Üê range_comp, val_comp_inclusion, image_univ, Subtype.range_coe,
            quasispectrum_eq_spectrum_union_zero]
      simp_rw [ContinuousMapZero.instUniformSpace, this, uniformity_comap,
        @inf_uniformity _ (.comap _ _) (.comap _ _), uniformity_comap, Filter.comap_inf,
        Filter.comap_comap]
      refine .symm <| inf_eq_left.mpr <| le_top.trans <| eq_top_iff.mp ?_
      have : ‚àÄ U ‚àà ùì§ (C(Unit, R)), (0, 0) ‚àà U := fun U hU ‚Ü¶ refl_mem_uniformity hU
      convert Filter.comap_const_of_mem this with ‚ü®u, v‚ü© <;>
      ext ‚ü®x, rfl‚ü© <;> [exact map_zero u; exact map_zero v]
    case map_id => exact cfcHom_id ha
    case map_spectrum =>
      intro f
      simp only [œà']
      rw [quasispectrum_eq_spectrum_union_zero]
      simp only [NonUnitalStarAlgHom.comp_assoc, NonUnitalStarAlgHom.comp_apply,
        NonUnitalStarAlgHom.coe_coe]
      rw [cfcHom_map_spectrum ha]
      ext x
      constructor
      ¬∑ rintro (‚ü®x, rfl‚ü© | rfl)
        ¬∑ exact ‚ü®‚ü®x.1, spectrum_subset_quasispectrum R a x.2‚ü©, rfl‚ü©
        ¬∑ exact ‚ü®0, map_zero f‚ü©
      ¬∑ rintro ‚ü®x, rfl‚ü©
        have hx := x.2
        simp_rw [quasispectrum_eq_spectrum_union_zero R a] at hx
        obtain (hx | hx) := hx
        ¬∑ exact Or.inl ‚ü®‚ü®x.1, hx‚ü©, rfl‚ü©
        ¬∑ apply Or.inr
          simp only [Set.mem_singleton_iff] at hx ‚ä¢
          rw [show x = 0 from Subtype.val_injective hx, map_zero]
    case predicate => exact fun f ‚Ü¶ cfcHom_predicate ha _

end UnitalToNonUnital
