/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro, Yury Kudryashov
-/
import Mathlib.Order.Filter.Pi
import Mathlib.Topology.Bases
import Mathlib.Data.Finset.Order
import Mathlib.Data.Set.Accumulate
import Mathlib.Data.Set.BoolIndicator
import Mathlib.Topology.Bornology.Basic
import Mathlib.Topology.LocallyFinite
import Mathlib.Order.Minimal

#align_import topology.subset_properties from "leanprover-community/mathlib"@"3efd324a3a31eaa40c9d5bfc669c4fafee5f9423"

/-!
# Properties of subsets of topological spaces

In this file we define various properties of subsets of a topological space, and some classes on
topological spaces.

## Main definitions

We define the following properties for sets in a topological space:

* `IsCompact`: each open cover has a finite subcover. This is defined in mathlib using filters.
  The main property of a compact set is `IsCompact.elim_finite_subcover`.
* `IsClopen`: a set that is both open and closed.
* `IsIrreducible`: a nonempty set that has contains no non-trivial pair of disjoint opens.
  See also the section below in the module doc.

For each of these definitions (except for `IsClopen`), we also have a class stating that the whole
space satisfies that property:
`CompactSpace`, `IrreducibleSpace`

Furthermore, we have three more classes:
* `LocallyCompactSpace`: for every point `x`, every open neighborhood of `x` contains a compact
  neighborhood of `x`. The definition is formulated in terms of the neighborhood filter.
* `SigmaCompactSpace`: a space that is the union of a countably many compact subspaces;
* `NoncompactSpace`: a space that is not a compact space.

## On the definition of irreducible and connected sets/spaces

In informal mathematics, irreducible spaces are assumed to be nonempty.
We formalise the predicate without that assumption as `IsPreirreducible`.
In other words, the only difference is whether the empty space counts as irreducible.
There are good reasons to consider the empty space to be ‚Äútoo simple to be simple‚Äù
See also https://ncatlab.org/nlab/show/too+simple+to+be+simple,
and in particular
https://ncatlab.org/nlab/show/too+simple+to+be+simple#relationship_to_biased_definitions.
-/


open Set Filter Topology TopologicalSpace Classical

universe u v

variable {Œ± : Type u} {Œ≤ : Type v} {Œπ : Type*} {œÄ : Œπ ‚Üí Type*}

variable [TopologicalSpace Œ±] [TopologicalSpace Œ≤] {s t : Set Œ±}

-- compact sets
section Compact

/-- A set `s` is compact if for every nontrivial filter `f` that contains `s`,
    there exists `a ‚àà s` such that every set of `f` meets every neighborhood of `a`. -/
def IsCompact (s : Set Œ±) :=
  ‚àÄ ‚¶Éf‚¶Ñ [NeBot f], f ‚â§ ùìü s ‚Üí ‚àÉ a ‚àà s, ClusterPt a f
#align is_compact IsCompact

/-- The complement to a compact set belongs to a filter `f` if it belongs to each filter
`ùìù a ‚äì f`, `a ‚àà s`. -/
theorem IsCompact.compl_mem_sets (hs : IsCompact s) {f : Filter Œ±} (hf : ‚àÄ a ‚àà s, s·∂ú ‚àà ùìù a ‚äì f) :
    s·∂ú ‚àà f := by
  contrapose! hf
  simp only [not_mem_iff_inf_principal_compl, compl_compl, inf_assoc] at hf ‚ä¢
  exact @hs _ hf inf_le_right
#align is_compact.compl_mem_sets IsCompact.compl_mem_sets

/-- The complement to a compact set belongs to a filter `f` if each `a ‚àà s` has a neighborhood `t`
within `s` such that `t·∂ú` belongs to `f`. -/
theorem IsCompact.compl_mem_sets_of_nhdsWithin (hs : IsCompact s) {f : Filter Œ±}
    (hf : ‚àÄ a ‚àà s, ‚àÉ t ‚àà ùìù[s] a, t·∂ú ‚àà f) : s·∂ú ‚àà f := by
  refine' hs.compl_mem_sets fun a ha => _
  rcases hf a ha with ‚ü®t, ht, hst‚ü©
  replace ht := mem_inf_principal.1 ht
  apply mem_inf_of_inter ht hst
  rintro x ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© hs
  exact h‚ÇÇ (h‚ÇÅ hs)
#align is_compact.compl_mem_sets_of_nhds_within IsCompact.compl_mem_sets_of_nhdsWithin

/-- If `p : Set Œ± ‚Üí Prop` is stable under restriction and union, and each point `x`
  of a compact set `s` has a neighborhood `t` within `s` such that `p t`, then `p s` holds. -/
@[elab_as_elim]
theorem IsCompact.induction_on {s : Set Œ±} (hs : IsCompact s) {p : Set Œ± ‚Üí Prop} (he : p ‚àÖ)
    (hmono : ‚àÄ ‚¶És t‚¶Ñ, s ‚äÜ t ‚Üí p t ‚Üí p s) (hunion : ‚àÄ ‚¶És t‚¶Ñ, p s ‚Üí p t ‚Üí p (s ‚à™ t))
    (hnhds : ‚àÄ x ‚àà s, ‚àÉ t ‚àà ùìù[s] x, p t) : p s := by
  let f : Filter Œ± :=
    { sets := { t | p t·∂ú }
      univ_sets := by simpa
      sets_of_superset := fun ht‚ÇÅ ht => hmono (compl_subset_compl.2 ht) ht‚ÇÅ
      inter_sets := fun ht‚ÇÅ ht‚ÇÇ => by simp [compl_inter, hunion ht‚ÇÅ ht‚ÇÇ] }
  have : s·∂ú ‚àà f := hs.compl_mem_sets_of_nhdsWithin (by simpa using hnhds)
  rwa [‚Üê compl_compl s]
#align is_compact.induction_on IsCompact.induction_on

/-- The intersection of a compact set and a closed set is a compact set. -/
theorem IsCompact.inter_right (hs : IsCompact s) (ht : IsClosed t) : IsCompact (s ‚à© t) := by
  intro f hnf hstf
  obtain ‚ü®a, hsa, ha‚ü© : ‚àÉ a ‚àà s, ClusterPt a f :=
    hs (le_trans hstf (le_principal_iff.2 (inter_subset_left _ _)))
  have : a ‚àà t := ht.mem_of_nhdsWithin_neBot <|
    ha.mono <| le_trans hstf (le_principal_iff.2 (inter_subset_right _ _))
  exact ‚ü®a, ‚ü®hsa, this‚ü©, ha‚ü©
#align is_compact.inter_right IsCompact.inter_right

/-- The intersection of a closed set and a compact set is a compact set. -/
theorem IsCompact.inter_left (ht : IsCompact t) (hs : IsClosed s) : IsCompact (s ‚à© t) :=
  inter_comm t s ‚ñ∏ ht.inter_right hs
#align is_compact.inter_left IsCompact.inter_left

/-- The set difference of a compact set and an open set is a compact set. -/
theorem IsCompact.diff (hs : IsCompact s) (ht : IsOpen t) : IsCompact (s \ t) :=
  hs.inter_right (isClosed_compl_iff.mpr ht)
#align is_compact.diff IsCompact.diff

/-- A closed subset of a compact set is a compact set. -/
theorem isCompact_of_isClosed_subset (hs : IsCompact s) (ht : IsClosed t) (h : t ‚äÜ s) :
    IsCompact t :=
  inter_eq_self_of_subset_right h ‚ñ∏ hs.inter_right ht
#align is_compact_of_is_closed_subset isCompact_of_isClosed_subset

theorem IsCompact.image_of_continuousOn {f : Œ± ‚Üí Œ≤} (hs : IsCompact s) (hf : ContinuousOn f s) :
    IsCompact (f '' s) := by
  intro l lne ls
  have : NeBot (l.comap f ‚äì ùìü s) :=
    comap_inf_principal_neBot_of_image_mem lne (le_principal_iff.1 ls)
  obtain ‚ü®a, has, ha‚ü© : ‚àÉ a ‚àà s, ClusterPt a (l.comap f ‚äì ùìü s) := @hs _ this inf_le_right
  haveI := ha.neBot
  use f a, mem_image_of_mem f has
  have : Tendsto f (ùìù a ‚äì (comap f l ‚äì ùìü s)) (ùìù (f a) ‚äì l) := by
    convert (hf a has).inf (@tendsto_comap _ _ f l) using 1
    rw [nhdsWithin]
    ac_rfl
  exact this.neBot
#align is_compact.image_of_continuous_on IsCompact.image_of_continuousOn

theorem IsCompact.image {f : Œ± ‚Üí Œ≤} (hs : IsCompact s) (hf : Continuous f) : IsCompact (f '' s) :=
  hs.image_of_continuousOn hf.continuousOn
#align is_compact.image IsCompact.image

theorem IsCompact.adherence_nhdset {f : Filter Œ±} (hs : IsCompact s) (hf‚ÇÇ : f ‚â§ ùìü s)
    (ht‚ÇÅ : IsOpen t) (ht‚ÇÇ : ‚àÄ a ‚àà s, ClusterPt a f ‚Üí a ‚àà t) : t ‚àà f :=
  Classical.by_cases mem_of_eq_bot fun (this : f ‚äì ùìü t·∂ú ‚â† ‚ä•) =>
    let ‚ü®a, ha, (hfa : ClusterPt a <| f ‚äì ùìü t·∂ú)‚ü© := @hs _ ‚ü®this‚ü© <| inf_le_of_left_le hf‚ÇÇ
    have : a ‚àà t := ht‚ÇÇ a ha hfa.of_inf_left
    have : t·∂ú ‚à© t ‚àà ùìù[t·∂ú] a := inter_mem_nhdsWithin _ (IsOpen.mem_nhds ht‚ÇÅ this)
    have A : ùìù[t·∂ú] a = ‚ä• := empty_mem_iff_bot.1 <| compl_inter_self t ‚ñ∏ this
    have : ùìù[t·∂ú] a ‚â† ‚ä• := hfa.of_inf_right.ne
    absurd A this
#align is_compact.adherence_nhdset IsCompact.adherence_nhdset

theorem isCompact_iff_ultrafilter_le_nhds :
    IsCompact s ‚Üî ‚àÄ f : Ultrafilter Œ±, ‚Üëf ‚â§ ùìü s ‚Üí ‚àÉ a ‚àà s, ‚Üëf ‚â§ ùìù a := by
  refine' (forall_neBot_le_iff _).trans _
  ¬∑ rintro f g hle ‚ü®a, has, haf‚ü©
    exact ‚ü®a, has, haf.mono hle‚ü©
  ¬∑ simp only [Ultrafilter.clusterPt_iff]
#align is_compact_iff_ultrafilter_le_nhds isCompact_iff_ultrafilter_le_nhds

alias ‚ü®IsCompact.ultrafilter_le_nhds, _‚ü© := isCompact_iff_ultrafilter_le_nhds
#align is_compact.ultrafilter_le_nhds IsCompact.ultrafilter_le_nhds

/-- For every open directed cover of a compact set, there exists a single element of the
cover which itself includes the set. -/
theorem IsCompact.elim_directed_cover {Œπ : Type v} [hŒπ : Nonempty Œπ] (hs : IsCompact s)
    (U : Œπ ‚Üí Set Œ±) (hUo : ‚àÄ i, IsOpen (U i)) (hsU : s ‚äÜ ‚ãÉ i, U i) (hdU : Directed (¬∑ ‚äÜ ¬∑) U) :
    ‚àÉ i, s ‚äÜ U i :=
  hŒπ.elim fun i‚ÇÄ =>
    IsCompact.induction_on hs ‚ü®i‚ÇÄ, empty_subset _‚ü© (fun _ _ hs ‚ü®i, hi‚ü© => ‚ü®i, hs.trans hi‚ü©)
      (fun _ _ ‚ü®i, hi‚ü© ‚ü®j, hj‚ü© =>
        let ‚ü®k, hki, hkj‚ü© := hdU i j
        ‚ü®k, union_subset (Subset.trans hi hki) (Subset.trans hj hkj)‚ü©)
      fun _x hx =>
      let ‚ü®i, hi‚ü© := mem_iUnion.1 (hsU hx)
      ‚ü®U i, mem_nhdsWithin_of_mem_nhds (IsOpen.mem_nhds (hUo i) hi), i, Subset.refl _‚ü©
#align is_compact.elim_directed_cover IsCompact.elim_directed_cover

/-- For every open cover of a compact set, there exists a finite subcover. -/
theorem IsCompact.elim_finite_subcover {Œπ : Type v} (hs : IsCompact s) (U : Œπ ‚Üí Set Œ±)
    (hUo : ‚àÄ i, IsOpen (U i)) (hsU : s ‚äÜ ‚ãÉ i, U i) : ‚àÉ t : Finset Œπ, s ‚äÜ ‚ãÉ i ‚àà t, U i :=
  hs.elim_directed_cover _ (fun _ => isOpen_biUnion fun i _ => hUo i)
    (iUnion_eq_iUnion_finset U ‚ñ∏ hsU) (directed_of_sup fun _ _ h => biUnion_subset_biUnion_left h)
#align is_compact.elim_finite_subcover IsCompact.elim_finite_subcover

theorem IsCompact.elim_nhds_subcover' (hs : IsCompact s) (U : ‚àÄ x ‚àà s, Set Œ±)
    (hU : ‚àÄ x (hx : x ‚àà s), U x ‚Äπx ‚àà s‚Ä∫ ‚àà ùìù x) : ‚àÉ t : Finset s, s ‚äÜ ‚ãÉ x ‚àà t, U (x : s) x.2 :=
  (hs.elim_finite_subcover (fun x : s => interior (U x x.2)) (fun _ => isOpen_interior) fun x hx =>
        mem_iUnion.2 ‚ü®‚ü®x, hx‚ü©, mem_interior_iff_mem_nhds.2 <| hU _ _‚ü©).imp
    fun _t ht => ht.trans <| iUnion‚ÇÇ_mono fun _ _ => interior_subset
#align is_compact.elim_nhds_subcover' IsCompact.elim_nhds_subcover'

theorem IsCompact.elim_nhds_subcover (hs : IsCompact s) (U : Œ± ‚Üí Set Œ±) (hU : ‚àÄ x ‚àà s, U x ‚àà ùìù x) :
    ‚àÉ t : Finset Œ±, (‚àÄ x ‚àà t, x ‚àà s) ‚àß s ‚äÜ ‚ãÉ x ‚àà t, U x :=
  let ‚ü®t, ht‚ü© := hs.elim_nhds_subcover' (fun x _ => U x) hU
  ‚ü®t.image (‚Üë), fun x hx =>
    let ‚ü®y, _, hyx‚ü© := Finset.mem_image.1 hx
    hyx ‚ñ∏ y.2,
    by rwa [Finset.set_biUnion_finset_image]‚ü©
#align is_compact.elim_nhds_subcover IsCompact.elim_nhds_subcover

/-- The neighborhood filter of a compact set is disjoint with a filter `l` if and only if the
neighborhood filter of each point of this set is disjoint with `l`. -/
theorem IsCompact.disjoint_nhdsSet_left {l : Filter Œ±} (hs : IsCompact s) :
    Disjoint (ùìùÀ¢ s) l ‚Üî ‚àÄ x ‚àà s, Disjoint (ùìù x) l := by
  refine' ‚ü®fun h x hx => h.mono_left <| nhds_le_nhdsSet hx, fun H => _‚ü©
  choose! U hxU hUl using fun x hx => (nhds_basis_opens x).disjoint_iff_left.1 (H x hx)
  choose hxU hUo using hxU
  rcases hs.elim_nhds_subcover U fun x hx => (hUo x hx).mem_nhds (hxU x hx) with ‚ü®t, hts, hst‚ü©
  refine'
    (hasBasis_nhdsSet _).disjoint_iff_left.2
      ‚ü®‚ãÉ x ‚àà t, U x, ‚ü®isOpen_biUnion fun x hx => hUo x (hts x hx), hst‚ü©, _‚ü©
  rw [compl_iUnion‚ÇÇ, biInter_finset_mem]
  exact fun x hx => hUl x (hts x hx)
#align is_compact.disjoint_nhds_set_left IsCompact.disjoint_nhdsSet_left

/-- A filter `l` is disjoint with the neighborhood filter of a compact set if and only if it is
disjoint with the neighborhood filter of each point of this set. -/
theorem IsCompact.disjoint_nhdsSet_right {l : Filter Œ±} (hs : IsCompact s) :
    Disjoint l (ùìùÀ¢ s) ‚Üî ‚àÄ x ‚àà s, Disjoint l (ùìù x) := by
  simpa only [disjoint_comm] using hs.disjoint_nhdsSet_left
#align is_compact.disjoint_nhds_set_right IsCompact.disjoint_nhdsSet_right

-- porting note: todo: reformulate using `Disjoint`
/-- For every directed family of closed sets whose intersection avoids a compact set,
there exists a single element of the family which itself avoids this compact set. -/
theorem IsCompact.elim_directed_family_closed {Œπ : Type v} [hŒπ : Nonempty Œπ] (hs : IsCompact s)
    (Z : Œπ ‚Üí Set Œ±) (hZc : ‚àÄ i, IsClosed (Z i)) (hsZ : (s ‚à© ‚ãÇ i, Z i) = ‚àÖ)
    (hdZ : Directed (¬∑ ‚äá ¬∑) Z) : ‚àÉ i : Œπ, s ‚à© Z i = ‚àÖ :=
  let ‚ü®t, ht‚ü© :=
    hs.elim_directed_cover (compl ‚àò Z) (fun i => (hZc i).isOpen_compl)
      (by
        simpa only [subset_def, not_forall, eq_empty_iff_forall_not_mem, mem_iUnion, exists_prop,
          mem_inter_iff, not_and, iff_self_iff, mem_iInter, mem_compl_iff] using hsZ)
      (hdZ.mono_comp _ fun _ _ => compl_subset_compl.mpr)
  ‚ü®t, by
    simpa only [subset_def, not_forall, eq_empty_iff_forall_not_mem, mem_iUnion, exists_prop,
      mem_inter_iff, not_and, iff_self_iff, mem_iInter, mem_compl_iff] using ht‚ü©
#align is_compact.elim_directed_family_closed IsCompact.elim_directed_family_closed

-- porting note: todo: reformulate using `Disjoint`
/-- For every family of closed sets whose intersection avoids a compact set,
there exists a finite subfamily whose intersection avoids this compact set. -/
theorem IsCompact.elim_finite_subfamily_closed {s : Set Œ±} {Œπ : Type v} (hs : IsCompact s)
    (Z : Œπ ‚Üí Set Œ±) (hZc : ‚àÄ i, IsClosed (Z i)) (hsZ : (s ‚à© ‚ãÇ i, Z i) = ‚àÖ) :
    ‚àÉ t : Finset Œπ, (s ‚à© ‚ãÇ i ‚àà t, Z i) = ‚àÖ :=
  hs.elim_directed_family_closed _ (fun t ‚Ü¶ isClosed_biInter fun _ _ ‚Ü¶ hZc _)
    (by rwa [‚Üê iInter_eq_iInter_finset]) (directed_of_sup fun _ _ h ‚Ü¶ biInter_subset_biInter_left h)
#align is_compact.elim_finite_subfamily_closed IsCompact.elim_finite_subfamily_closed

/-- If `s` is a compact set in a topological space `Œ±` and `f : Œπ ‚Üí Set Œ±` is a locally finite
family of sets, then `f i ‚à© s` is nonempty only for a finitely many `i`. -/
theorem LocallyFinite.finite_nonempty_inter_compact {Œπ : Type*} {f : Œπ ‚Üí Set Œ±}
    (hf : LocallyFinite f) {s : Set Œ±} (hs : IsCompact s) : { i | (f i ‚à© s).Nonempty }.Finite := by
  choose U hxU hUf using hf
  rcases hs.elim_nhds_subcover U fun x _ => hxU x with ‚ü®t, -, hsU‚ü©
  refine' (t.finite_toSet.biUnion fun x _ => hUf x).subset _
  rintro i ‚ü®x, hx‚ü©
  rcases mem_iUnion‚ÇÇ.1 (hsU hx.2) with ‚ü®c, hct, hcx‚ü©
  exact mem_biUnion hct ‚ü®x, hx.1, hcx‚ü©
#align locally_finite.finite_nonempty_inter_compact LocallyFinite.finite_nonempty_inter_compact

/-- To show that a compact set intersects the intersection of a family of closed sets,
  it is sufficient to show that it intersects every finite subfamily. -/
theorem IsCompact.inter_iInter_nonempty {s : Set Œ±} {Œπ : Type v} (hs : IsCompact s) (Z : Œπ ‚Üí Set Œ±)
    (hZc : ‚àÄ i, IsClosed (Z i)) (hsZ : ‚àÄ t : Finset Œπ, (s ‚à© ‚ãÇ i ‚àà t, Z i).Nonempty) :
    (s ‚à© ‚ãÇ i, Z i).Nonempty := by
  simp only [nonempty_iff_ne_empty] at hsZ ‚ä¢
  apply mt (hs.elim_finite_subfamily_closed Z hZc); push_neg; exact hsZ
#align is_compact.inter_Inter_nonempty IsCompact.inter_iInter_nonempty

/-- Cantor's intersection theorem:
the intersection of a directed family of nonempty compact closed sets is nonempty. -/
theorem IsCompact.nonempty_iInter_of_directed_nonempty_compact_closed {Œπ : Type v} [hŒπ : Nonempty Œπ]
    (Z : Œπ ‚Üí Set Œ±) (hZd : Directed (¬∑ ‚äá ¬∑) Z) (hZn : ‚àÄ i, (Z i).Nonempty)
    (hZc : ‚àÄ i, IsCompact (Z i)) (hZcl : ‚àÄ i, IsClosed (Z i)) : (‚ãÇ i, Z i).Nonempty := by
  let i‚ÇÄ := hŒπ.some
  suffices (Z i‚ÇÄ ‚à© ‚ãÇ i, Z i).Nonempty by
    rwa [inter_eq_right_iff_subset.mpr (iInter_subset _ i‚ÇÄ)] at this
  simp only [nonempty_iff_ne_empty] at hZn ‚ä¢
  apply mt ((hZc i‚ÇÄ).elim_directed_family_closed Z hZcl)
  push_neg
  simp only [‚Üê nonempty_iff_ne_empty] at hZn ‚ä¢
  refine' ‚ü®hZd, fun i => _‚ü©
  rcases hZd i‚ÇÄ i with ‚ü®j, hji‚ÇÄ, hji‚ü©
  exact (hZn j).mono (subset_inter hji‚ÇÄ hji)
#align is_compact.nonempty_Inter_of_directed_nonempty_compact_closed IsCompact.nonempty_iInter_of_directed_nonempty_compact_closed

/-- Cantor's intersection theorem for sequences indexed by `‚Ñï`:
the intersection of a decreasing sequence of nonempty compact closed sets is nonempty. -/
theorem IsCompact.nonempty_iInter_of_sequence_nonempty_compact_closed (Z : ‚Ñï ‚Üí Set Œ±)
    (hZd : ‚àÄ i, Z (i + 1) ‚äÜ Z i) (hZn : ‚àÄ i, (Z i).Nonempty) (hZ0 : IsCompact (Z 0))
    (hZcl : ‚àÄ i, IsClosed (Z i)) : (‚ãÇ i, Z i).Nonempty :=
  have Zmono : Antitone Z := antitone_nat_of_succ_le hZd
  have hZd : Directed (¬∑ ‚äá ¬∑) Z := directed_of_sup Zmono
  have : ‚àÄ i, Z i ‚äÜ Z 0 := fun i => Zmono <| zero_le i
  have hZc : ‚àÄ i, IsCompact (Z i) := fun i => isCompact_of_isClosed_subset hZ0 (hZcl i) (this i)
  IsCompact.nonempty_iInter_of_directed_nonempty_compact_closed Z hZd hZn hZc hZcl
#align is_compact.nonempty_Inter_of_sequence_nonempty_compact_closed IsCompact.nonempty_iInter_of_sequence_nonempty_compact_closed

/-- For every open cover of a compact set, there exists a finite subcover. -/
theorem IsCompact.elim_finite_subcover_image {b : Set Œπ} {c : Œπ ‚Üí Set Œ±} (hs : IsCompact s)
    (hc‚ÇÅ : ‚àÄ i ‚àà b, IsOpen (c i)) (hc‚ÇÇ : s ‚äÜ ‚ãÉ i ‚àà b, c i) :
    ‚àÉ b', b' ‚äÜ b ‚àß Set.Finite b' ‚àß s ‚äÜ ‚ãÉ i ‚àà b', c i := by
  simp only [Subtype.forall', biUnion_eq_iUnion] at hc‚ÇÅ hc‚ÇÇ
  rcases hs.elim_finite_subcover (fun i => c i : b ‚Üí Set Œ±) hc‚ÇÅ hc‚ÇÇ with ‚ü®d, hd‚ü©
  refine' ‚ü®Subtype.val '' d.toSet, _, d.finite_toSet.image _, _‚ü©
  ¬∑ simp
  ¬∑ rwa [biUnion_image]
#align is_compact.elim_finite_subcover_image IsCompact.elim_finite_subcover_image‚Çì

/-- A set `s` is compact if for every open cover of `s`, there exists a finite subcover. -/
theorem isCompact_of_finite_subcover
    (h : ‚àÄ {Œπ : Type u} (U : Œπ ‚Üí Set Œ±), (‚àÄ i, IsOpen (U i)) ‚Üí (s ‚äÜ ‚ãÉ i, U i) ‚Üí
      ‚àÉ t : Finset Œπ, s ‚äÜ ‚ãÉ i ‚àà t, U i) :
    IsCompact s := fun f hf hfs => by
  contrapose! h
  simp only [ClusterPt, not_neBot, ‚Üê disjoint_iff, SetCoe.forall',
    (nhds_basis_opens _).disjoint_iff_left] at h
  choose U hU hUf using h
  refine ‚ü®s, U, fun x => (hU x).2, fun x hx => mem_iUnion.2 ‚ü®‚ü®x, hx‚ü©, (hU _).1‚ü©, fun t ht => ?_‚ü©
  refine compl_not_mem (le_principal_iff.1 hfs) ?_
  refine mem_of_superset ((biInter_finset_mem t).2 fun x _ => hUf x) ?_
  rw [subset_compl_comm, compl_iInter‚ÇÇ]
  simpa only [compl_compl]
#align is_compact_of_finite_subcover isCompact_of_finite_subcover

-- porting note: todo: reformulate using `Disjoint`
/-- A set `s` is compact if for every family of closed sets whose intersection avoids `s`,
there exists a finite subfamily whose intersection avoids `s`. -/
theorem isCompact_of_finite_subfamily_closed
    (h : ‚àÄ {Œπ : Type u} (Z : Œπ ‚Üí Set Œ±), (‚àÄ i, IsClosed (Z i)) ‚Üí (s ‚à© ‚ãÇ i, Z i) = ‚àÖ ‚Üí
      ‚àÉ t : Finset Œπ, (s ‚à© ‚ãÇ i ‚àà t, Z i) = ‚àÖ) :
    IsCompact s :=
  isCompact_of_finite_subcover fun U hUo hsU => by
    rw [‚Üê disjoint_compl_right_iff_subset, compl_iUnion, disjoint_iff] at hsU
    rcases h (fun i => (U i)·∂ú) (fun i => (hUo _).isClosed_compl) hsU with ‚ü®t, ht‚ü©
    refine ‚ü®t, ?_‚ü©
    rwa [‚Üê disjoint_compl_right_iff_subset, compl_iUnion‚ÇÇ, disjoint_iff]
#align is_compact_of_finite_subfamily_closed isCompact_of_finite_subfamily_closed

/-- A set `s` is compact if and only if
for every open cover of `s`, there exists a finite subcover. -/
theorem isCompact_iff_finite_subcover :
    IsCompact s ‚Üî ‚àÄ {Œπ : Type u} (U : Œπ ‚Üí Set Œ±),
      (‚àÄ i, IsOpen (U i)) ‚Üí (s ‚äÜ ‚ãÉ i, U i) ‚Üí ‚àÉ t : Finset Œπ, s ‚äÜ ‚ãÉ i ‚àà t, U i :=
  ‚ü®fun hs => hs.elim_finite_subcover, isCompact_of_finite_subcover‚ü©
#align is_compact_iff_finite_subcover isCompact_iff_finite_subcover

/-- A set `s` is compact if and only if
for every family of closed sets whose intersection avoids `s`,
there exists a finite subfamily whose intersection avoids `s`. -/
theorem isCompact_iff_finite_subfamily_closed :
    IsCompact s ‚Üî ‚àÄ {Œπ : Type u} (Z : Œπ ‚Üí Set Œ±),
      (‚àÄ i, IsClosed (Z i)) ‚Üí (s ‚à© ‚ãÇ i, Z i) = ‚àÖ ‚Üí ‚àÉ t : Finset Œπ, (s ‚à© ‚ãÇ i ‚àà t, Z i) = ‚àÖ :=
  ‚ü®fun hs => hs.elim_finite_subfamily_closed, isCompact_of_finite_subfamily_closed‚ü©
#align is_compact_iff_finite_subfamily_closed isCompact_iff_finite_subfamily_closed

/-- To show that `‚àÄ y ‚àà K, P x y` holds for `x` close enough to `x‚ÇÄ` when `K` is compact,
it is sufficient to show that for all `y‚ÇÄ ‚àà K` there `P x y` holds for `(x, y)` close enough
to `(x‚ÇÄ, y‚ÇÄ)`.
-/
theorem IsCompact.eventually_forall_of_forall_eventually {x‚ÇÄ : Œ±} {K : Set Œ≤} (hK : IsCompact K)
    {P : Œ± ‚Üí Œ≤ ‚Üí Prop} (hP : ‚àÄ y ‚àà K, ‚àÄ·∂† z : Œ± √ó Œ≤ in ùìù (x‚ÇÄ, y), P z.1 z.2) :
    ‚àÄ·∂† x in ùìù x‚ÇÄ, ‚àÄ y ‚àà K, P x y := by
  refine' hK.induction_on _ _ _ _
  ¬∑ exact eventually_of_forall fun x y => False.elim
  ¬∑ intro s t hst ht
    refine' ht.mono fun x h y hys => h y <| hst hys
  ¬∑ intro s t hs ht
    filter_upwards [hs, ht]
    rintro x h1 h2 y (hys | hyt)
    exacts [h1 y hys, h2 y hyt]
  ¬∑ intro y hyK
    specialize hP y hyK
    rw [nhds_prod_eq, eventually_prod_iff] at hP
    rcases hP with ‚ü®p, hp, q, hq, hpq‚ü©
    exact ‚ü®{ y | q y }, mem_nhdsWithin_of_mem_nhds hq, eventually_of_mem hp @hpq‚ü©
#align is_compact.eventually_forall_of_forall_eventually IsCompact.eventually_forall_of_forall_eventually

@[simp]
theorem isCompact_empty : IsCompact (‚àÖ : Set Œ±) := fun _f hnf hsf =>
  Not.elim hnf.ne <| empty_mem_iff_bot.1 <| le_principal_iff.1 hsf
#align is_compact_empty isCompact_empty

@[simp]
theorem isCompact_singleton {a : Œ±} : IsCompact ({a} : Set Œ±) := fun f hf hfa =>
  ‚ü®a, rfl, ClusterPt.of_le_nhds'
    (hfa.trans <| by simpa only [principal_singleton] using pure_le_nhds a) hf‚ü©
#align is_compact_singleton isCompact_singleton

theorem Set.Subsingleton.isCompact {s : Set Œ±} (hs : s.Subsingleton) : IsCompact s :=
  Subsingleton.induction_on hs isCompact_empty fun _ => isCompact_singleton
#align set.subsingleton.is_compact Set.Subsingleton.isCompact

-- porting note: golfed a proof instead of fixing it
theorem Set.Finite.isCompact_biUnion {s : Set Œπ} {f : Œπ ‚Üí Set Œ±} (hs : s.Finite)
    (hf : ‚àÄ i ‚àà s, IsCompact (f i)) : IsCompact (‚ãÉ i ‚àà s, f i) :=
  isCompact_iff_ultrafilter_le_nhds.2 <| fun l hl => by
    rw [le_principal_iff, Ultrafilter.mem_coe, Ultrafilter.finite_biUnion_mem_iff hs] at hl
    rcases hl with ‚ü®i, his, hi‚ü©
    rcases (hf i his).ultrafilter_le_nhds _ (le_principal_iff.2 hi) with ‚ü®x, hxi, hlx‚ü©
    exact ‚ü®x, mem_iUnion‚ÇÇ.2 ‚ü®i, his, hxi‚ü©, hlx‚ü©
#align set.finite.is_compact_bUnion Set.Finite.isCompact_biUnion

theorem Finset.isCompact_biUnion (s : Finset Œπ) {f : Œπ ‚Üí Set Œ±} (hf : ‚àÄ i ‚àà s, IsCompact (f i)) :
    IsCompact (‚ãÉ i ‚àà s, f i) :=
  s.finite_toSet.isCompact_biUnion hf
#align finset.is_compact_bUnion Finset.isCompact_biUnion

theorem isCompact_accumulate {K : ‚Ñï ‚Üí Set Œ±} (hK : ‚àÄ n, IsCompact (K n)) (n : ‚Ñï) :
    IsCompact (Accumulate K n) :=
  (finite_le_nat n).isCompact_biUnion fun k _ => hK k
#align is_compact_accumulate isCompact_accumulate

-- porting note: new lemma
theorem Set.Finite.isCompact_sUnion {S : Set (Set Œ±)} (hf : S.Finite) (hc : ‚àÄ s ‚àà S, IsCompact s) :
    IsCompact (‚ãÉ‚ÇÄ S) := by
  rw [sUnion_eq_biUnion]; exact hf.isCompact_biUnion hc

-- porting note: generalized to `Œπ : Sort*`
theorem isCompact_iUnion {Œπ : Sort*} {f : Œπ ‚Üí Set Œ±} [Finite Œπ] (h : ‚àÄ i, IsCompact (f i)) :
    IsCompact (‚ãÉ i, f i) :=
  (finite_range f).isCompact_sUnion <| forall_range_iff.2 h
#align is_compact_Union isCompact_iUnion

theorem Set.Finite.isCompact (hs : s.Finite) : IsCompact s :=
  biUnion_of_singleton s ‚ñ∏ hs.isCompact_biUnion fun _ _ => isCompact_singleton
#align set.finite.is_compact Set.Finite.isCompact

theorem IsCompact.finite_of_discrete [DiscreteTopology Œ±] {s : Set Œ±} (hs : IsCompact s) :
    s.Finite := by
  have : ‚àÄ x : Œ±, ({x} : Set Œ±) ‚àà ùìù x := by simp [nhds_discrete]
  rcases hs.elim_nhds_subcover (fun x => {x}) fun x _ => this x with ‚ü®t, _, hst‚ü©
  simp only [‚Üê t.set_biUnion_coe, biUnion_of_singleton] at hst
  exact t.finite_toSet.subset hst
#align is_compact.finite_of_discrete IsCompact.finite_of_discrete

theorem isCompact_iff_finite [DiscreteTopology Œ±] {s : Set Œ±} : IsCompact s ‚Üî s.Finite :=
  ‚ü®fun h => h.finite_of_discrete, fun h => h.isCompact‚ü©
#align is_compact_iff_finite isCompact_iff_finite

theorem IsCompact.union (hs : IsCompact s) (ht : IsCompact t) : IsCompact (s ‚à™ t) := by
  rw [union_eq_iUnion]; exact isCompact_iUnion fun b => by cases b <;> assumption
#align is_compact.union IsCompact.union

protected theorem IsCompact.insert (hs : IsCompact s) (a) : IsCompact (insert a s) :=
  isCompact_singleton.union hs
#align is_compact.insert IsCompact.insert

-- porting note: todo: reformulate using `ùìùÀ¢`
/-- If `V : Œπ ‚Üí Set Œ±` is a decreasing family of closed compact sets then any neighborhood of
`‚ãÇ i, V i` contains some `V i`. We assume each `V i` is compact *and* closed because `Œ±` is
not assumed to be Hausdorff. See `exists_subset_nhd_of_compact` for version assuming this. -/
theorem exists_subset_nhds_of_isCompact' {Œπ : Type*} [Nonempty Œπ] {V : Œπ ‚Üí Set Œ±}
    (hV : Directed (¬∑ ‚äá ¬∑) V) (hV_cpct : ‚àÄ i, IsCompact (V i)) (hV_closed : ‚àÄ i, IsClosed (V i))
    {U : Set Œ±} (hU : ‚àÄ x ‚àà ‚ãÇ i, V i, U ‚àà ùìù x) : ‚àÉ i, V i ‚äÜ U := by
  obtain ‚ü®W, hsubW, W_op, hWU‚ü© := exists_open_set_nhds hU
  suffices : ‚àÉ i, V i ‚äÜ W
  ¬∑ exact this.imp fun i hi => hi.trans hWU
  by_contra' H
  replace H : ‚àÄ i, (V i ‚à© W·∂ú).Nonempty := fun i => Set.inter_compl_nonempty_iff.mpr (H i)
  have : (‚ãÇ i, V i ‚à© W·∂ú).Nonempty := by
    refine'
      IsCompact.nonempty_iInter_of_directed_nonempty_compact_closed _ (fun i j => _) H
        (fun i => (hV_cpct i).inter_right W_op.isClosed_compl) fun i =>
        (hV_closed i).inter W_op.isClosed_compl
    rcases hV i j with ‚ü®k, hki, hkj‚ü©
    refine' ‚ü®k, ‚ü®fun x => _, fun x => _‚ü©‚ü© <;> simp only [and_imp, mem_inter_iff, mem_compl_iff] <;>
      tauto
  have : ¬¨‚ãÇ i : Œπ, V i ‚äÜ W := by simpa [‚Üê iInter_inter, inter_compl_nonempty_iff]
  contradiction
#align exists_subset_nhds_of_is_compact' exists_subset_nhds_of_isCompact'

/-- If `Œ±` has a basis consisting of compact opens, then an open set in `Œ±` is compact open iff
  it is a finite union of some elements in the basis -/
theorem isCompact_open_iff_eq_finite_iUnion_of_isTopologicalBasis (b : Œπ ‚Üí Set Œ±)
    (hb : IsTopologicalBasis (Set.range b)) (hb' : ‚àÄ i, IsCompact (b i)) (U : Set Œ±) :
    IsCompact U ‚àß IsOpen U ‚Üî ‚àÉ s : Set Œπ, s.Finite ‚àß U = ‚ãÉ i ‚àà s, b i := by
  constructor
  ¬∑ rintro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©
    obtain ‚ü®Œ≤, f, e, hf‚ü© := hb.open_eq_iUnion h‚ÇÇ
    choose f' hf' using hf
    have : b ‚àò f' = f := funext hf'
    subst this
    obtain ‚ü®t, ht‚ü© :=
      h‚ÇÅ.elim_finite_subcover (b ‚àò f') (fun i => hb.isOpen (Set.mem_range_self _)) (by rw [e])
    refine' ‚ü®t.image f', Set.Finite.intro inferInstance, le_antisymm _ _‚ü©
    ¬∑ refine' Set.Subset.trans ht _
      simp only [Set.iUnion_subset_iff]
      intro i hi
      erw [‚Üê Set.iUnion_subtype (fun x : Œπ => x ‚àà t.image f') fun i => b i.1]
      exact Set.subset_iUnion (fun i : t.image f' => b i) ‚ü®_, Finset.mem_image_of_mem _ hi‚ü©
    ¬∑ apply Set.iUnion‚ÇÇ_subset
      rintro i hi
      obtain ‚ü®j, -, rfl‚ü© := Finset.mem_image.mp hi
      rw [e]
      exact Set.subset_iUnion (b ‚àò f') j
  ¬∑ rintro ‚ü®s, hs, rfl‚ü©
    constructor
    ¬∑ exact hs.isCompact_biUnion fun i _ => hb' i
    ¬∑ exact isOpen_biUnion fun i _ => hb.isOpen (Set.mem_range_self _)
#align is_compact_open_iff_eq_finite_Union_of_is_topological_basis isCompact_open_iff_eq_finite_iUnion_of_isTopologicalBasis

namespace Filter

/-- `Filter.cocompact` is the filter generated by complements to compact sets. -/
def cocompact (Œ± : Type*) [TopologicalSpace Œ±] : Filter Œ± :=
  ‚®Ö (s : Set Œ±) (_ : IsCompact s), ùìü s·∂ú
#align filter.cocompact Filter.cocompact

theorem hasBasis_cocompact : (cocompact Œ±).HasBasis IsCompact compl :=
  hasBasis_biInf_principal'
    (fun s hs t ht =>
      ‚ü®s ‚à™ t, hs.union ht, compl_subset_compl.2 (subset_union_left s t),
        compl_subset_compl.2 (subset_union_right s t)‚ü©)
    ‚ü®‚àÖ, isCompact_empty‚ü©
#align filter.has_basis_cocompact Filter.hasBasis_cocompact

theorem mem_cocompact : s ‚àà cocompact Œ± ‚Üî ‚àÉ t, IsCompact t ‚àß t·∂ú ‚äÜ s :=
  hasBasis_cocompact.mem_iff
#align filter.mem_cocompact Filter.mem_cocompact

theorem mem_cocompact' : s ‚àà cocompact Œ± ‚Üî ‚àÉ t, IsCompact t ‚àß s·∂ú ‚äÜ t :=
  mem_cocompact.trans <| exists_congr fun _ => and_congr_right fun _ => compl_subset_comm
#align filter.mem_cocompact' Filter.mem_cocompact'

theorem _root_.IsCompact.compl_mem_cocompact (hs : IsCompact s) : s·∂ú ‚àà Filter.cocompact Œ± :=
  hasBasis_cocompact.mem_of_mem hs
#align is_compact.compl_mem_cocompact IsCompact.compl_mem_cocompact

theorem cocompact_le_cofinite : cocompact Œ± ‚â§ cofinite := fun s hs =>
  compl_compl s ‚ñ∏ hs.isCompact.compl_mem_cocompact
#align filter.cocompact_le_cofinite Filter.cocompact_le_cofinite

theorem cocompact_eq_cofinite (Œ± : Type*) [TopologicalSpace Œ±] [DiscreteTopology Œ±] :
    cocompact Œ± = cofinite := by
  simp only [cocompact, hasBasis_cofinite.eq_biInf, isCompact_iff_finite]
#align filter.cocompact_eq_cofinite Filter.cocompact_eq_cofinite

@[simp] theorem _root_.Nat.cocompact_eq : cocompact ‚Ñï = atTop :=
  (cocompact_eq_cofinite ‚Ñï).trans Nat.cofinite_eq_atTop
#align nat.cocompact_eq Nat.cocompact_eq

theorem Tendsto.isCompact_insert_range_of_cocompact {f : Œ± ‚Üí Œ≤} {b}
    (hf : Tendsto f (cocompact Œ±) (ùìù b)) (hfc : Continuous f) : IsCompact (insert b (range f)) := by
  intro l hne hle
  by_cases hb : ClusterPt b l
  ¬∑ exact ‚ü®b, Or.inl rfl, hb‚ü©
  simp only [clusterPt_iff, not_forall, ‚Üê not_disjoint_iff_nonempty_inter, not_not] at hb
  rcases hb with ‚ü®s, hsb, t, htl, hd‚ü©
  rcases mem_cocompact.1 (hf hsb) with ‚ü®K, hKc, hKs‚ü©
  have : f '' K ‚àà l := by
    filter_upwards [htl, le_principal_iff.1 hle]with y hyt hyf
    rcases hyf with (rfl | ‚ü®x, rfl‚ü©)
    exacts [(hd.le_bot ‚ü®mem_of_mem_nhds hsb, hyt‚ü©).elim,
      mem_image_of_mem _ (not_not.1 fun hxK => hd.le_bot ‚ü®hKs hxK, hyt‚ü©)]
  rcases hKc.image hfc (le_principal_iff.2 this) with ‚ü®y, hy, hyl‚ü©
  exact ‚ü®y, Or.inr <| image_subset_range _ _ hy, hyl‚ü©
#align filter.tendsto.is_compact_insert_range_of_cocompact Filter.Tendsto.isCompact_insert_range_of_cocompact

theorem Tendsto.isCompact_insert_range_of_cofinite {f : Œπ ‚Üí Œ±} {a} (hf : Tendsto f cofinite (ùìù a)) :
    IsCompact (insert a (range f)) := by
  letI : TopologicalSpace Œπ := ‚ä•; haveI h : DiscreteTopology Œπ := ‚ü®rfl‚ü©
  rw [‚Üê cocompact_eq_cofinite Œπ] at hf
  exact hf.isCompact_insert_range_of_cocompact continuous_of_discreteTopology
#align filter.tendsto.is_compact_insert_range_of_cofinite Filter.Tendsto.isCompact_insert_range_of_cofinite

theorem Tendsto.isCompact_insert_range {f : ‚Ñï ‚Üí Œ±} {a} (hf : Tendsto f atTop (ùìù a)) :
    IsCompact (insert a (range f)) :=
  Filter.Tendsto.isCompact_insert_range_of_cofinite <| Nat.cofinite_eq_atTop.symm ‚ñ∏ hf
#align filter.tendsto.is_compact_insert_range Filter.Tendsto.isCompact_insert_range

/-- `Filter.coclosedCompact` is the filter generated by complements to closed compact sets.
In a Hausdorff space, this is the same as `Filter.cocompact`. -/
def coclosedCompact (Œ± : Type*) [TopologicalSpace Œ±] : Filter Œ± :=
  ‚®Ö (s : Set Œ±) (_ : IsClosed s) (_ : IsCompact s), ùìü s·∂ú
#align filter.coclosed_compact Filter.coclosedCompact

theorem hasBasis_coclosedCompact :
    (Filter.coclosedCompact Œ±).HasBasis (fun s => IsClosed s ‚àß IsCompact s) compl := by
  simp only [Filter.coclosedCompact, iInf_and']
  refine' hasBasis_biInf_principal' _ ‚ü®‚àÖ, isClosed_empty, isCompact_empty‚ü©
  rintro s ‚ü®hs‚ÇÅ, hs‚ÇÇ‚ü© t ‚ü®ht‚ÇÅ, ht‚ÇÇ‚ü©
  exact ‚ü®s ‚à™ t, ‚ü®‚ü®hs‚ÇÅ.union ht‚ÇÅ, hs‚ÇÇ.union ht‚ÇÇ‚ü©, compl_subset_compl.2 (subset_union_left _ _),
    compl_subset_compl.2 (subset_union_right _ _)‚ü©‚ü©
#align filter.has_basis_coclosed_compact Filter.hasBasis_coclosedCompact

theorem mem_coclosedCompact : s ‚àà coclosedCompact Œ± ‚Üî ‚àÉ t, IsClosed t ‚àß IsCompact t ‚àß t·∂ú ‚äÜ s := by
  simp only [hasBasis_coclosedCompact.mem_iff, and_assoc]
#align filter.mem_coclosed_compact Filter.mem_coclosedCompact

theorem mem_coclosed_compact' : s ‚àà coclosedCompact Œ± ‚Üî ‚àÉ t, IsClosed t ‚àß IsCompact t ‚àß s·∂ú ‚äÜ t := by
  simp only [mem_coclosedCompact, compl_subset_comm]
#align filter.mem_coclosed_compact' Filter.mem_coclosed_compact'

theorem cocompact_le_coclosedCompact : cocompact Œ± ‚â§ coclosedCompact Œ± :=
  iInf_mono fun _ => le_iInf fun _ => le_rfl
#align filter.cocompact_le_coclosed_compact Filter.cocompact_le_coclosedCompact

end Filter

theorem IsCompact.compl_mem_coclosedCompact_of_isClosed (hs : IsCompact s) (hs' : IsClosed s) :
    s·∂ú ‚àà Filter.coclosedCompact Œ± :=
  hasBasis_coclosedCompact.mem_of_mem ‚ü®hs', hs‚ü©
#align is_compact.compl_mem_coclosed_compact_of_is_closed IsCompact.compl_mem_coclosedCompact_of_isClosed

namespace Bornology

variable (Œ±)

/-- Sets that are contained in a compact set form a bornology. Its `cobounded` filter is
`Filter.cocompact`. See also `Bornology.relativelyCompact` the bornology of sets with compact
closure. -/
def inCompact : Bornology Œ± where
  cobounded' := Filter.cocompact Œ±
  le_cofinite' := Filter.cocompact_le_cofinite
#align bornology.in_compact Bornology.inCompact

variable {Œ±}

theorem inCompact.isBounded_iff : @IsBounded _ (inCompact Œ±) s ‚Üî ‚àÉ t, IsCompact t ‚àß s ‚äÜ t := by
  change s·∂ú ‚àà Filter.cocompact Œ± ‚Üî _
  rw [Filter.mem_cocompact]
  simp
#align bornology.in_compact.is_bounded_iff Bornology.inCompact.isBounded_iff

end Bornology

section TubeLemma

/-- `NhdsContainBoxes s t` means that any open neighborhood of `s √ó t` in `Œ± √ó Œ≤` includes
a product of an open neighborhood of `s` by an open neighborhood of `t`. -/
def NhdsContainBoxes (s : Set Œ±) (t : Set Œ≤) : Prop :=
  ‚àÄ n : Set (Œ± √ó Œ≤), IsOpen n ‚Üí (s √óÀ¢ t ‚äÜ n) ‚Üí
    ‚àÉ (u : Set Œ±) (v : Set Œ≤), IsOpen u ‚àß IsOpen v ‚àß s ‚äÜ u ‚àß t ‚äÜ v ‚àß u √óÀ¢ v ‚äÜ n
#align nhds_contain_boxes NhdsContainBoxes

@[symm]
theorem NhdsContainBoxes.symm {s : Set Œ±} {t : Set Œ≤} :
    NhdsContainBoxes s t ‚Üí NhdsContainBoxes t s := fun H n hn hp =>
  let ‚ü®u, v, uo, vo, su, tv, p‚ü© :=
    H (Prod.swap ‚Åª¬π' n) (hn.preimage continuous_swap) (by rwa [‚Üê image_subset_iff, image_swap_prod])
  ‚ü®v, u, vo, uo, tv, su, by rwa [‚Üê image_subset_iff, image_swap_prod] at p‚ü©
#align nhds_contain_boxes.symm NhdsContainBoxes.symm

theorem NhdsContainBoxes.comm {s : Set Œ±} {t : Set Œ≤} :
    NhdsContainBoxes s t ‚Üî NhdsContainBoxes t s :=
  Iff.intro NhdsContainBoxes.symm NhdsContainBoxes.symm
#align nhds_contain_boxes.comm NhdsContainBoxes.comm

theorem nhdsContainBoxes_of_singleton {x : Œ±} {y : Œ≤} :
    NhdsContainBoxes ({x} : Set Œ±) ({y} : Set Œ≤) := fun n hn hp =>
  let ‚ü®u, v, uo, vo, xu, yv, hp'‚ü© := isOpen_prod_iff.mp hn x y (hp <| by simp)
  ‚ü®u, v, uo, vo, by simpa, by simpa, hp'‚ü©
#align nhds_contain_boxes_of_singleton nhdsContainBoxes_of_singleton

theorem nhdsContainBoxes_of_compact {s : Set Œ±} (hs : IsCompact s) (t : Set Œ≤)
    (H : ‚àÄ x ‚àà s, NhdsContainBoxes ({x} : Set Œ±) t) : NhdsContainBoxes s t := fun n hn hp =>
  have :
    ‚àÄ x : s,
      ‚àÉ uv : Set Œ± √ó Set Œ≤, IsOpen uv.1 ‚àß IsOpen uv.2 ‚àß {‚Üëx} ‚äÜ uv.1 ‚àß t ‚äÜ uv.2 ‚àß uv.1 √óÀ¢ uv.2 ‚äÜ n :=
    fun ‚ü®x, hx‚ü© =>
    have : ({x} : Set Œ±) √óÀ¢ t ‚äÜ n := Subset.trans (prod_mono (by simpa) Subset.rfl) hp
    let ‚ü®ux, vx, H1‚ü© := H x hx n hn this
    ‚ü®‚ü®ux, vx‚ü©, H1‚ü©
  let ‚ü®uvs, h‚ü© := Classical.axiom_of_choice this
  have us_cover : s ‚äÜ ‚ãÉ i, (uvs i).1 := fun x hx =>
    subset_iUnion _ ‚ü®x, hx‚ü© (by simpa using (h ‚ü®x, hx‚ü©).2.2.1)
  let ‚ü®s0, s0_cover‚ü© := hs.elim_finite_subcover _ (fun i => (h i).1) us_cover
  let u := ‚ãÉ i ‚àà s0, (uvs i).1
  let v := ‚ãÇ i ‚àà s0, (uvs i).2
  have : IsOpen u := isOpen_biUnion fun i _ => (h i).1
  have : IsOpen v := isOpen_biInter s0.finite_toSet fun i _ => (h i).2.1
  have : t ‚äÜ v := subset_iInter‚ÇÇ fun i _ => (h i).2.2.2.1
  have : u √óÀ¢ v ‚äÜ n := fun ‚ü®x', y'‚ü© ‚ü®hx', hy'‚ü© =>
    have : ‚àÉ i ‚àà s0, x' ‚àà (uvs i).1 := by simpa using hx'
    let ‚ü®i, is0, hi‚ü© := this
    (h i).2.2.2.2 ‚ü®hi, (biInter_subset_of_mem is0 : v ‚äÜ (uvs i).2) hy'‚ü©
  ‚ü®u, v, ‚ÄπIsOpen u‚Ä∫, ‚ÄπIsOpen v‚Ä∫, s0_cover, ‚Äπt ‚äÜ v‚Ä∫, ‚Äπu √óÀ¢ v ‚äÜ n‚Ä∫‚ü©
#align nhds_contain_boxes_of_compact nhdsContainBoxes_of_compact

/-- If `s` and `t` are compact sets and `n` is an open neighborhood of `s √ó t`, then there exist
open neighborhoods `u ‚äá s` and `v ‚äá t` such that `u √ó v ‚äÜ n`. -/
theorem generalized_tube_lemma {s : Set Œ±} (hs : IsCompact s) {t : Set Œ≤} (ht : IsCompact t)
    {n : Set (Œ± √ó Œ≤)} (hn : IsOpen n) (hp : s √óÀ¢ t ‚äÜ n) :
    ‚àÉ (u : Set Œ±) (v : Set Œ≤), IsOpen u ‚àß IsOpen v ‚àß s ‚äÜ u ‚àß t ‚äÜ v ‚àß u √óÀ¢ v ‚äÜ n :=
  have := nhdsContainBoxes_of_compact hs t fun x _ => NhdsContainBoxes.symm <|
    nhdsContainBoxes_of_compact ht {x} fun _ _ => nhdsContainBoxes_of_singleton
  this n hn hp
#align generalized_tube_lemma generalized_tube_lemma

end TubeLemma

/-- Type class for compact spaces. Separation is sometimes included in the definition, especially
in the French literature, but we do not include it here. -/
class CompactSpace (Œ± : Type*) [TopologicalSpace Œ±] : Prop where
  /-- In a compact space, `Set.univ` is a compact set. -/
  isCompact_univ : IsCompact (univ : Set Œ±)
#align compact_space CompactSpace

-- see Note [lower instance priority]
instance (priority := 10) Subsingleton.compactSpace [Subsingleton Œ±] : CompactSpace Œ± :=
  ‚ü®subsingleton_univ.isCompact‚ü©
#align subsingleton.compact_space Subsingleton.compactSpace

theorem isCompact_univ_iff : IsCompact (univ : Set Œ±) ‚Üî CompactSpace Œ± :=
  ‚ü®fun h => ‚ü®h‚ü©, fun h => h.1‚ü©
#align is_compact_univ_iff isCompact_univ_iff

theorem isCompact_univ [h : CompactSpace Œ±] : IsCompact (univ : Set Œ±) :=
  h.isCompact_univ
#align is_compact_univ isCompact_univ

theorem cluster_point_of_compact [CompactSpace Œ±] (f : Filter Œ±) [NeBot f] : ‚àÉ x, ClusterPt x f :=
  by simpa using isCompact_univ (show f ‚â§ ùìü univ by simp)
#align cluster_point_of_compact cluster_point_of_compact

theorem CompactSpace.elim_nhds_subcover [CompactSpace Œ±] (U : Œ± ‚Üí Set Œ±) (hU : ‚àÄ x, U x ‚àà ùìù x) :
    ‚àÉ t : Finset Œ±, ‚ãÉ x ‚àà t, U x = ‚ä§ := by
  obtain ‚ü®t, -, s‚ü© := IsCompact.elim_nhds_subcover isCompact_univ U fun x _ => hU x
  exact ‚ü®t, top_unique s‚ü©
#align compact_space.elim_nhds_subcover CompactSpace.elim_nhds_subcover

theorem compactSpace_of_finite_subfamily_closed
    (h : ‚àÄ {Œπ : Type u} (Z : Œπ ‚Üí Set Œ±), (‚àÄ i, IsClosed (Z i)) ‚Üí ‚ãÇ i, Z i = ‚àÖ ‚Üí
      ‚àÉ t : Finset Œπ, ‚ãÇ i ‚àà t, Z i = ‚àÖ) :
    CompactSpace Œ± where
  isCompact_univ := isCompact_of_finite_subfamily_closed fun Z => by
    simpa using h Z
#align compact_space_of_finite_subfamily_closed compactSpace_of_finite_subfamily_closed

theorem IsClosed.isCompact [CompactSpace Œ±] {s : Set Œ±} (h : IsClosed s) : IsCompact s :=
  isCompact_of_isClosed_subset isCompact_univ h (subset_univ _)
#align is_closed.is_compact IsClosed.isCompact

/-- `Œ±` is a noncompact topological space if it is not a compact space. -/
class NoncompactSpace (Œ± : Type*) [TopologicalSpace Œ±] : Prop where
  /-- In a noncompact space, `Set.univ` is not a compact set. -/
  noncompact_univ : ¬¨IsCompact (univ : Set Œ±)
#align noncompact_space NoncompactSpace

-- porting note: a lemma instead of `export` to make `Œ±` explicit
lemma noncompact_univ (Œ± : Type*) [TopologicalSpace Œ±] [NoncompactSpace Œ±] :
    ¬¨IsCompact (univ : Set Œ±) :=
  NoncompactSpace.noncompact_univ

theorem IsCompact.ne_univ [NoncompactSpace Œ±] {s : Set Œ±} (hs : IsCompact s) : s ‚â† univ := fun h =>
  noncompact_univ Œ± (h ‚ñ∏ hs)
#align is_compact.ne_univ IsCompact.ne_univ

instance [NoncompactSpace Œ±] : NeBot (Filter.cocompact Œ±) := by
  refine' Filter.hasBasis_cocompact.neBot_iff.2 fun hs => _
  contrapose hs; rw [not_nonempty_iff_eq_empty, compl_empty_iff] at hs
  rw [hs]; exact noncompact_univ Œ±

@[simp]
theorem Filter.cocompact_eq_bot [CompactSpace Œ±] : Filter.cocompact Œ± = ‚ä• :=
  Filter.hasBasis_cocompact.eq_bot_iff.mpr ‚ü®Set.univ, isCompact_univ, Set.compl_univ‚ü©
#align filter.cocompact_eq_bot Filter.cocompact_eq_bot

instance [NoncompactSpace Œ±] : NeBot (Filter.coclosedCompact Œ±) :=
  neBot_of_le Filter.cocompact_le_coclosedCompact

theorem noncompactSpace_of_neBot (_ : NeBot (Filter.cocompact Œ±)) : NoncompactSpace Œ± :=
  ‚ü®fun h' => (Filter.nonempty_of_mem h'.compl_mem_cocompact).ne_empty compl_univ‚ü©
#align noncompact_space_of_ne_bot noncompactSpace_of_neBot

theorem Filter.cocompact_neBot_iff : NeBot (Filter.cocompact Œ±) ‚Üî NoncompactSpace Œ± :=
  ‚ü®noncompactSpace_of_neBot, fun _ => inferInstance‚ü©
#align filter.cocompact_ne_bot_iff Filter.cocompact_neBot_iff

theorem not_compactSpace_iff : ¬¨CompactSpace Œ± ‚Üî NoncompactSpace Œ± :=
  ‚ü®fun h‚ÇÅ => ‚ü®fun h‚ÇÇ => h‚ÇÅ ‚ü®h‚ÇÇ‚ü©‚ü©, fun ‚ü®h‚ÇÅ‚ü© ‚ü®h‚ÇÇ‚ü© => h‚ÇÅ h‚ÇÇ‚ü©
#align not_compact_space_iff not_compactSpace_iff

instance : NoncompactSpace ‚Ñ§ :=
  noncompactSpace_of_neBot <| by simp only [Filter.cocompact_eq_cofinite, Filter.cofinite_neBot]

-- Note: We can't make this into an instance because it loops with `Finite.compactSpace`.
/-- A compact discrete space is finite. -/
theorem finite_of_compact_of_discrete [CompactSpace Œ±] [DiscreteTopology Œ±] : Finite Œ± :=
  Finite.of_finite_univ <| isCompact_univ.finite_of_discrete
#align finite_of_compact_of_discrete finite_of_compact_of_discrete

theorem exists_nhds_ne_neBot (Œ± : Type*) [TopologicalSpace Œ±] [CompactSpace Œ±] [Infinite Œ±] :
    ‚àÉ z : Œ±, (ùìù[‚â†] z).NeBot := by
  by_contra' H
  simp_rw [not_neBot] at H
  haveI := discreteTopology_iff_nhds_ne.2 H
  exact Infinite.not_finite (finite_of_compact_of_discrete : Finite Œ±)
#align exists_nhds_ne_ne_bot exists_nhds_ne_neBot

theorem finite_cover_nhds_interior [CompactSpace Œ±] {U : Œ± ‚Üí Set Œ±} (hU : ‚àÄ x, U x ‚àà ùìù x) :
    ‚àÉ t : Finset Œ±, ‚ãÉ x ‚àà t, interior (U x) = univ :=
  let ‚ü®t, ht‚ü© := isCompact_univ.elim_finite_subcover (fun x => interior (U x))
    (fun _ => isOpen_interior) fun x _ => mem_iUnion.2 ‚ü®x, mem_interior_iff_mem_nhds.2 (hU x)‚ü©
  ‚ü®t, univ_subset_iff.1 ht‚ü©
#align finite_cover_nhds_interior finite_cover_nhds_interior

theorem finite_cover_nhds [CompactSpace Œ±] {U : Œ± ‚Üí Set Œ±} (hU : ‚àÄ x, U x ‚àà ùìù x) :
    ‚àÉ t : Finset Œ±, ‚ãÉ x ‚àà t, U x = univ :=
  let ‚ü®t, ht‚ü© := finite_cover_nhds_interior hU
  ‚ü®t, univ_subset_iff.1 <| ht.symm.subset.trans <| iUnion‚ÇÇ_mono fun _ _ => interior_subset‚ü©
#align finite_cover_nhds finite_cover_nhds

/-- If `Œ±` is a compact space, then a locally finite family of sets of `Œ±` can have only finitely
many nonempty elements. -/
theorem LocallyFinite.finite_nonempty_of_compact {Œπ : Type*} [CompactSpace Œ±] {f : Œπ ‚Üí Set Œ±}
    (hf : LocallyFinite f) : { i | (f i).Nonempty }.Finite := by
  simpa only [inter_univ] using hf.finite_nonempty_inter_compact isCompact_univ
#align locally_finite.finite_nonempty_of_compact LocallyFinite.finite_nonempty_of_compact

/-- If `Œ±` is a compact space, then a locally finite family of nonempty sets of `Œ±` can have only
finitely many elements, `Set.Finite` version. -/
theorem LocallyFinite.finite_of_compact {Œπ : Type*} [CompactSpace Œ±] {f : Œπ ‚Üí Set Œ±}
    (hf : LocallyFinite f) (hne : ‚àÄ i, (f i).Nonempty) : (univ : Set Œπ).Finite := by
  simpa only [hne] using hf.finite_nonempty_of_compact
#align locally_finite.finite_of_compact LocallyFinite.finite_of_compact

/-- If `Œ±` is a compact space, then a locally finite family of nonempty sets of `Œ±` can have only
finitely many elements, `Fintype` version. -/
noncomputable def LocallyFinite.fintypeOfCompact {Œπ : Type*} [CompactSpace Œ±] {f : Œπ ‚Üí Set Œ±}
    (hf : LocallyFinite f) (hne : ‚àÄ i, (f i).Nonempty) : Fintype Œπ :=
  fintypeOfFiniteUniv (hf.finite_of_compact hne)
#align locally_finite.fintype_of_compact LocallyFinite.fintypeOfCompact

/-- The comap of the cocompact filter on `Œ≤` by a continuous function `f : Œ± ‚Üí Œ≤` is less than or
equal to the cocompact filter on `Œ±`.
This is a reformulation of the fact that images of compact sets are compact. -/
theorem Filter.comap_cocompact_le {f : Œ± ‚Üí Œ≤} (hf : Continuous f) :
    (Filter.cocompact Œ≤).comap f ‚â§ Filter.cocompact Œ± := by
  rw [(Filter.hasBasis_cocompact.comap f).le_basis_iff Filter.hasBasis_cocompact]
  intro t ht
  refine' ‚ü®f '' t, ht.image hf, _‚ü©
  simpa using t.subset_preimage_image f
#align filter.comap_cocompact_le Filter.comap_cocompact_le

theorem isCompact_range [CompactSpace Œ±] {f : Œ± ‚Üí Œ≤} (hf : Continuous f) : IsCompact (range f) := by
  rw [‚Üê image_univ]; exact isCompact_univ.image hf
#align is_compact_range isCompact_range

theorem isCompact_diagonal [CompactSpace Œ±] : IsCompact (diagonal Œ±) :=
  @range_diag Œ± ‚ñ∏ isCompact_range (continuous_id.prod_mk continuous_id)
#align is_compact_diagonal isCompact_diagonal

-- porting note: renamed, golfed
/-- If `X` is a compact topological space, then `Prod.snd : X √ó Y ‚Üí Y` is a closed map. -/
theorem isClosedMap_snd_of_compactSpace {X : Type*} [TopologicalSpace X] [CompactSpace X]
    {Y : Type*} [TopologicalSpace Y] : IsClosedMap (Prod.snd : X √ó Y ‚Üí Y) := fun s hs => by
  rw [‚Üê isOpen_compl_iff, isOpen_iff_mem_nhds]
  intro y hy
  have : univ √óÀ¢ {y} ‚äÜ s·∂ú
  ¬∑ exact fun (x, y') ‚ü®_, rfl‚ü© hs => hy ‚ü®(x, y'), hs, rfl‚ü©
  rcases generalized_tube_lemma isCompact_univ isCompact_singleton hs.isOpen_compl this
    with ‚ü®U, V, -, hVo, hU, hV, hs‚ü©
  refine mem_nhds_iff.2 ‚ü®V, ?_, hVo, hV rfl‚ü©
  rintro _ hzV ‚ü®z, hzs, rfl‚ü©
  exact hs ‚ü®hU trivial, hzV‚ü© hzs
#align is_closed_proj_of_is_compact isClosedMap_snd_of_compactSpace

theorem exists_subset_nhds_of_compactSpace [CompactSpace Œ±] {Œπ : Type*} [Nonempty Œπ]
    {V : Œπ ‚Üí Set Œ±} (hV : Directed (¬∑ ‚äá ¬∑) V) (hV_closed : ‚àÄ i, IsClosed (V i)) {U : Set Œ±}
    (hU : ‚àÄ x ‚àà ‚ãÇ i, V i, U ‚àà ùìù x) : ‚àÉ i, V i ‚äÜ U :=
  exists_subset_nhds_of_isCompact' hV (fun i => (hV_closed i).isCompact) hV_closed hU
#align exists_subset_nhds_of_compact_space exists_subset_nhds_of_compactSpace

/-- If `f : Œ± ‚Üí Œ≤` is an `Inducing` map, then the image `f '' s` of a set `s` is compact if and only
if the set `s` is closed. -/
theorem Inducing.isCompact_iff {f : Œ± ‚Üí Œ≤} (hf : Inducing f) {s : Set Œ±} :
    IsCompact (f '' s) ‚Üî IsCompact s := by
  refine ‚ü®fun hs F F_ne_bot F_le => ?_, fun hs => hs.image hf.continuous‚ü©
  obtain ‚ü®_, ‚ü®x, x_in : x ‚àà s, rfl‚ü©, hx : ClusterPt (f x) (map f F)‚ü© :=
    hs ((map_mono F_le).trans_eq map_principal)
  exact ‚ü®x, x_in, hf.mapClusterPt_iff.1 hx‚ü©
#align inducing.is_compact_iff Inducing.isCompact_iff

/-- If `f : Œ± ‚Üí Œ≤` is an `Embedding` (or more generally, an `Inducing` map, see
`Inducing.isCompact_iff`), then the image `f '' s` of a set `s` is compact if and only if the set
`s` is closed. -/
theorem Embedding.isCompact_iff_isCompact_image {f : Œ± ‚Üí Œ≤} (hf : Embedding f) :
    IsCompact s ‚Üî IsCompact (f '' s) :=
  hf.toInducing.isCompact_iff.symm
#align embedding.is_compact_iff_is_compact_image Embedding.isCompact_iff_isCompact_image

/-- The preimage of a compact set under a closed embedding is a compact set. -/
theorem ClosedEmbedding.isCompact_preimage {f : Œ± ‚Üí Œ≤} (hf : ClosedEmbedding f) {K : Set Œ≤}
    (hK : IsCompact K) : IsCompact (f ‚Åª¬π' K) := by
  replace hK := hK.inter_right hf.closed_range
  rwa [‚Üê hf.toInducing.isCompact_iff, image_preimage_eq_inter_range]
#align closed_embedding.is_compact_preimage ClosedEmbedding.isCompact_preimage

/-- A closed embedding is proper, ie, inverse images of compact sets are contained in compacts.
Moreover, the preimage of a compact set is compact, see `ClosedEmbedding.isCompact_preimage`. -/
theorem ClosedEmbedding.tendsto_cocompact {f : Œ± ‚Üí Œ≤} (hf : ClosedEmbedding f) :
    Tendsto f (Filter.cocompact Œ±) (Filter.cocompact Œ≤) :=
  Filter.hasBasis_cocompact.tendsto_right_iff.mpr fun _K hK =>
    (hf.isCompact_preimage hK).compl_mem_cocompact
#align closed_embedding.tendsto_cocompact ClosedEmbedding.tendsto_cocompact

theorem isCompact_iff_isCompact_in_subtype {p : Œ± ‚Üí Prop} {s : Set { a // p a }} :
    IsCompact s ‚Üî IsCompact (((‚Üë) : _ ‚Üí Œ±) '' s) :=
  embedding_subtype_val.isCompact_iff_isCompact_image
#align is_compact_iff_is_compact_in_subtype isCompact_iff_isCompact_in_subtype

theorem isCompact_iff_isCompact_univ {s : Set Œ±} : IsCompact s ‚Üî IsCompact (univ : Set s) := by
  rw [isCompact_iff_isCompact_in_subtype, image_univ, Subtype.range_coe]
#align is_compact_iff_is_compact_univ isCompact_iff_isCompact_univ

theorem isCompact_iff_compactSpace {s : Set Œ±} : IsCompact s ‚Üî CompactSpace s :=
  isCompact_iff_isCompact_univ.trans isCompact_univ_iff
#align is_compact_iff_compact_space isCompact_iff_compactSpace

theorem IsCompact.finite {s : Set Œ±} (hs : IsCompact s) (hs' : DiscreteTopology s) : s.Finite :=
  finite_coe_iff.mp (@finite_of_compact_of_discrete _ _ (isCompact_iff_compactSpace.mp hs) hs')
#align is_compact.finite IsCompact.finite

theorem exists_nhds_ne_inf_principal_neBot {s : Set Œ±} (hs : IsCompact s) (hs' : s.Infinite) :
    ‚àÉ z ‚àà s, (ùìù[‚â†] z ‚äì ùìü s).NeBot := by
  by_contra' H
  simp_rw [not_neBot] at H
  exact hs' (hs.finite <| discreteTopology_subtype_iff.mpr H)
#align exists_nhds_ne_inf_principal_ne_bot exists_nhds_ne_inf_principal_neBot

protected theorem ClosedEmbedding.noncompactSpace [NoncompactSpace Œ±] {f : Œ± ‚Üí Œ≤}
    (hf : ClosedEmbedding f) : NoncompactSpace Œ≤ :=
  noncompactSpace_of_neBot hf.tendsto_cocompact.neBot
#align closed_embedding.noncompact_space ClosedEmbedding.noncompactSpace

protected theorem ClosedEmbedding.compactSpace [h : CompactSpace Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : ClosedEmbedding f) : CompactSpace Œ± :=
  ‚ü®by rw [‚Üê hf.toInducing.isCompact_iff, image_univ]; exact hf.closed_range.isCompact‚ü©
#align closed_embedding.compact_space ClosedEmbedding.compactSpace

theorem IsCompact.prod {s : Set Œ±} {t : Set Œ≤} (hs : IsCompact s) (ht : IsCompact t) :
    IsCompact (s √óÀ¢ t) := by
  rw [isCompact_iff_ultrafilter_le_nhds] at hs ht ‚ä¢
  intro f hfs
  rw [le_principal_iff] at hfs
  obtain ‚ü®a : Œ±, sa : a ‚àà s, ha : map Prod.fst f.1 ‚â§ ùìù a‚ü© :=
    hs (f.map Prod.fst) (le_principal_iff.2 <| mem_map.2 <| mem_of_superset hfs fun x => And.left)
  obtain ‚ü®b : Œ≤, tb : b ‚àà t, hb : map Prod.snd f.1 ‚â§ ùìù b‚ü© :=
    ht (f.map Prod.snd) (le_principal_iff.2 <| mem_map.2 <| mem_of_superset hfs fun x => And.right)
  rw [map_le_iff_le_comap] at ha hb
  refine' ‚ü®‚ü®a, b‚ü©, ‚ü®sa, tb‚ü©, _‚ü©
  rw [nhds_prod_eq]; exact le_inf ha hb
#align is_compact.prod IsCompact.prod

/-- Finite topological spaces are compact. -/
instance (priority := 100) Finite.compactSpace [Finite Œ±] : CompactSpace Œ±
    where isCompact_univ := finite_univ.isCompact
#align finite.compact_space Finite.compactSpace

/-- The product of two compact spaces is compact. -/
instance [CompactSpace Œ±] [CompactSpace Œ≤] : CompactSpace (Œ± √ó Œ≤) :=
  ‚ü®by rw [‚Üê univ_prod_univ]; exact isCompact_univ.prod isCompact_univ‚ü©

/-- The disjoint union of two compact spaces is compact. -/
instance [CompactSpace Œ±] [CompactSpace Œ≤] : CompactSpace (Œ± ‚äï Œ≤) :=
  ‚ü®by
    rw [‚Üê range_inl_union_range_inr]
    exact (isCompact_range continuous_inl).union (isCompact_range continuous_inr)‚ü©

instance [Finite Œπ] [‚àÄ i, TopologicalSpace (œÄ i)] [‚àÄ i, CompactSpace (œÄ i)] :
    CompactSpace (Œ£i, œÄ i) := by
  refine' ‚ü®_‚ü©
  rw [Sigma.univ]
  exact isCompact_iUnion fun i => isCompact_range continuous_sigmaMk

/-- The coproduct of the cocompact filters on two topological spaces is the cocompact filter on
their product. -/
theorem Filter.coprod_cocompact :
    (Filter.cocompact Œ±).coprod (Filter.cocompact Œ≤) = Filter.cocompact (Œ± √ó Œ≤) := by
  ext S
  simp only [mem_coprod_iff, exists_prop, mem_comap, Filter.mem_cocompact]
  constructor
  ¬∑ rintro ‚ü®‚ü®A, ‚ü®t, ht, hAt‚ü©, hAS‚ü©, B, ‚ü®t', ht', hBt'‚ü©, hBS‚ü©
    refine' ‚ü®t √óÀ¢ t', ht.prod ht', _‚ü©
    refine' Subset.trans _ (union_subset hAS hBS)
    rw [compl_subset_comm] at hAt hBt' ‚ä¢
    refine' Subset.trans (fun x => _) (Set.prod_mono hAt hBt')
    simp only [compl_union, mem_inter_iff, mem_prod, mem_preimage, mem_compl_iff]
    tauto
  ¬∑ rintro ‚ü®t, ht, htS‚ü©
    refine' ‚ü®‚ü®(Prod.fst '' t)·∂ú, _, _‚ü©, ‚ü®(Prod.snd '' t)·∂ú, _, _‚ü©‚ü©
    ¬∑ exact ‚ü®Prod.fst '' t, ht.image continuous_fst, Subset.rfl‚ü©
    ¬∑ rw [preimage_compl]
      rw [compl_subset_comm] at htS ‚ä¢
      exact htS.trans (subset_preimage_image Prod.fst _)
    ¬∑ exact ‚ü®Prod.snd '' t, ht.image continuous_snd, Subset.rfl‚ü©
    ¬∑ rw [preimage_compl]
      rw [compl_subset_comm] at htS ‚ä¢
      exact htS.trans (subset_preimage_image Prod.snd _)
#align filter.coprod_cocompact Filter.coprod_cocompact

theorem Prod.noncompactSpace_iff :
    NoncompactSpace (Œ± √ó Œ≤) ‚Üî NoncompactSpace Œ± ‚àß Nonempty Œ≤ ‚à® Nonempty Œ± ‚àß NoncompactSpace Œ≤ := by
  simp [‚Üê Filter.cocompact_neBot_iff, ‚Üê Filter.coprod_cocompact, Filter.coprod_neBot_iff]
#align prod.noncompact_space_iff Prod.noncompactSpace_iff

-- See Note [lower instance priority]
instance (priority := 100) Prod.noncompactSpace_left [NoncompactSpace Œ±] [Nonempty Œ≤] :
    NoncompactSpace (Œ± √ó Œ≤) :=
  Prod.noncompactSpace_iff.2 (Or.inl ‚ü®‚Äπ_‚Ä∫, ‚Äπ_‚Ä∫‚ü©)
#align prod.noncompact_space_left Prod.noncompactSpace_left

-- See Note [lower instance priority]
instance (priority := 100) Prod.noncompactSpace_right [Nonempty Œ±] [NoncompactSpace Œ≤] :
    NoncompactSpace (Œ± √ó Œ≤) :=
  Prod.noncompactSpace_iff.2 (Or.inr ‚ü®‚Äπ_‚Ä∫, ‚Äπ_‚Ä∫‚ü©)
#align prod.noncompact_space_right Prod.noncompactSpace_right

section Tychonoff

variable [‚àÄ i, TopologicalSpace (œÄ i)]

/-- **Tychonoff's theorem**: product of compact sets is compact. -/
theorem isCompact_pi_infinite {s : ‚àÄ i, Set (œÄ i)} :
    (‚àÄ i, IsCompact (s i)) ‚Üí IsCompact { x : ‚àÄ i, œÄ i | ‚àÄ i, x i ‚àà s i } := by
  simp only [isCompact_iff_ultrafilter_le_nhds, nhds_pi, Filter.pi, exists_prop, mem_setOf_eq,
    le_iInf_iff, le_principal_iff]
  intro h f hfs
  have : ‚àÄ i : Œπ, ‚àÉ a, a ‚àà s i ‚àß Tendsto (Function.eval i) f (ùìù a) := by
    refine fun i => h i (f.map _) (mem_map.2 ?_)
    exact mem_of_superset hfs fun x hx => hx i
  choose a ha using this
  exact ‚ü®a, fun i => (ha i).left, fun i => (ha i).right.le_comap‚ü©
#align is_compact_pi_infinite isCompact_pi_infinite

/-- **Tychonoff's theorem** formulated using `Set.pi`: product of compact sets is compact. -/
theorem isCompact_univ_pi {s : ‚àÄ i, Set (œÄ i)} (h : ‚àÄ i, IsCompact (s i)) :
    IsCompact (pi univ s) := by
  convert isCompact_pi_infinite h
  simp only [‚Üê mem_univ_pi, setOf_mem_eq]
#align is_compact_univ_pi isCompact_univ_pi

instance Pi.compactSpace [‚àÄ i, CompactSpace (œÄ i)] : CompactSpace (‚àÄ i, œÄ i) :=
  ‚ü®by rw [‚Üê pi_univ univ]; exact isCompact_univ_pi fun i => isCompact_univ‚ü©
#align pi.compact_space Pi.compactSpace

instance Function.compactSpace [CompactSpace Œ≤] : CompactSpace (Œπ ‚Üí Œ≤) :=
  Pi.compactSpace
#align function.compact_space Function.compactSpace

/-- **Tychonoff's theorem** formulated in terms of filters: `Filter.cocompact` on an indexed product
type `Œ† d, Œ∫ d` the `Filter.coprod·µ¢` of filters `Filter.cocompact` on `Œ∫ d`. -/
theorem Filter.coprod·µ¢_cocompact {Œ¥ : Type*} {Œ∫ : Œ¥ ‚Üí Type*} [‚àÄ d, TopologicalSpace (Œ∫ d)] :
    (Filter.coprod·µ¢ fun d => Filter.cocompact (Œ∫ d)) = Filter.cocompact (‚àÄ d, Œ∫ d) := by
  refine' le_antisymm (iSup_le fun i => Filter.comap_cocompact_le (continuous_apply i)) _
  refine' compl_surjective.forall.2 fun s H => _
  simp only [compl_mem_coprod·µ¢, Filter.mem_cocompact, compl_subset_compl, image_subset_iff] at H ‚ä¢
  choose K hKc htK using H
  exact ‚ü®Set.pi univ K, isCompact_univ_pi hKc, fun f hf i _ => htK i hf‚ü©
set_option linter.uppercaseLean3 false in
#align filter.Coprod_cocompact Filter.coprod·µ¢_cocompact

end Tychonoff

instance Quot.compactSpace {r : Œ± ‚Üí Œ± ‚Üí Prop} [CompactSpace Œ±] : CompactSpace (Quot r) :=
  ‚ü®by
    rw [‚Üê range_quot_mk]
    exact isCompact_range continuous_quot_mk‚ü©
#align quot.compact_space Quot.compactSpace

instance Quotient.compactSpace {s : Setoid Œ±} [CompactSpace Œ±] : CompactSpace (Quotient s) :=
  Quot.compactSpace
#align quotient.compact_space Quotient.compactSpace

/-- There are various definitions of "locally compact space" in the literature, which agree for
Hausdorff spaces but not in general. This one is the precise condition on X needed for the
evaluation `map C(X, Y) √ó X ‚Üí Y` to be continuous for all `Y` when `C(X, Y)` is given the
compact-open topology. -/
class LocallyCompactSpace (Œ± : Type*) [TopologicalSpace Œ±] : Prop where
  /-- In a locally compact space,
    every neighbourhood of every point contains a compact neighbourhood of that same point. -/
  local_compact_nhds : ‚àÄ (x : Œ±), ‚àÄ n ‚àà ùìù x, ‚àÉ s ‚àà ùìù x, s ‚äÜ n ‚àß IsCompact s
#align locally_compact_space LocallyCompactSpace

theorem compact_basis_nhds [LocallyCompactSpace Œ±] (x : Œ±) :
    (ùìù x).HasBasis (fun s => s ‚àà ùìù x ‚àß IsCompact s) fun s => s :=
  hasBasis_self.2 <| by simpa only [and_comm] using LocallyCompactSpace.local_compact_nhds x
#align compact_basis_nhds compact_basis_nhds

theorem local_compact_nhds [LocallyCompactSpace Œ±] {x : Œ±} {n : Set Œ±} (h : n ‚àà ùìù x) :
    ‚àÉ s ‚àà ùìù x, s ‚äÜ n ‚àß IsCompact s :=
  LocallyCompactSpace.local_compact_nhds _ _ h
#align local_compact_nhds local_compact_nhds

/-- In a locally compact space, the filters `ùìù x` and `cocompact Œ±` are disjoint for all `Œ±`. -/
theorem disjoint_nhds_cocompact [LocallyCompactSpace Œ±] (x : Œ±) : Disjoint (ùìù x) (cocompact Œ±) :=
  let ‚ü®_, hx, _, hc‚ü© := local_compact_nhds (univ_mem (f := ùìù x));
  disjoint_of_disjoint_of_mem disjoint_compl_right hx hc.compl_mem_cocompact

theorem locallyCompactSpace_of_hasBasis {Œπ : Œ± ‚Üí Type*} {p : ‚àÄ x, Œπ x ‚Üí Prop}
    {s : ‚àÄ x, Œπ x ‚Üí Set Œ±} (h : ‚àÄ x, (ùìù x).HasBasis (p x) (s x))
    (hc : ‚àÄ x i, p x i ‚Üí IsCompact (s x i)) : LocallyCompactSpace Œ± :=
  ‚ü®fun x _t ht =>
    let ‚ü®i, hp, ht‚ü© := (h x).mem_iff.1 ht
    ‚ü®s x i, (h x).mem_of_mem hp, ht, hc x i hp‚ü©‚ü©
#align locally_compact_space_of_has_basis locallyCompactSpace_of_hasBasis

instance Prod.locallyCompactSpace (Œ± : Type*) (Œ≤ : Type*) [TopologicalSpace Œ±]
    [TopologicalSpace Œ≤] [LocallyCompactSpace Œ±] [LocallyCompactSpace Œ≤] :
    LocallyCompactSpace (Œ± √ó Œ≤) :=
  have := fun x : Œ± √ó Œ≤ => (compact_basis_nhds x.1).prod_nhds' (compact_basis_nhds x.2)
  locallyCompactSpace_of_hasBasis this fun _ _ ‚ü®‚ü®_, h‚ÇÅ‚ü©, _, h‚ÇÇ‚ü© => h‚ÇÅ.prod h‚ÇÇ
#align prod.locally_compact_space Prod.locallyCompactSpace

section Pi

variable [‚àÄ i, TopologicalSpace (œÄ i)] [‚àÄ i, LocallyCompactSpace (œÄ i)]

/-- In general it suffices that all but finitely many of the spaces are compact,
  but that's not straightforward to state and use. -/
instance Pi.locallyCompactSpace_of_finite [Finite Œπ] : LocallyCompactSpace (‚àÄ i, œÄ i) :=
  ‚ü®fun t n hn => by
    rw [nhds_pi, Filter.mem_pi] at hn
    obtain ‚ü®s, -, n', hn', hsub‚ü© := hn
    choose n'' hn'' hsub' hc using fun i =>
      LocallyCompactSpace.local_compact_nhds (t i) (n' i) (hn' i)
    refine' ‚ü®(Set.univ : Set Œπ).pi n'', _, subset_trans (fun _ h => _) hsub, isCompact_univ_pi hc‚ü©
    ¬∑ exact (set_pi_mem_nhds_iff (@Set.finite_univ Œπ _) _).mpr fun i _ => hn'' i
    ¬∑ exact fun i _ => hsub' i (h i trivial)‚ü©
#align pi.locally_compact_space_of_finite Pi.locallyCompactSpace_of_finite

/-- For spaces that are not Hausdorff. -/
instance Pi.locallyCompactSpace [‚àÄ i, CompactSpace (œÄ i)] : LocallyCompactSpace (‚àÄ i, œÄ i) :=
  ‚ü®fun t n hn => by
    rw [nhds_pi, Filter.mem_pi] at hn
    obtain ‚ü®s, hs, n', hn', hsub‚ü© := hn
    choose n'' hn'' hsub' hc using fun i =>
      LocallyCompactSpace.local_compact_nhds (t i) (n' i) (hn' i)
    refine' ‚ü®s.pi n'', _, subset_trans (fun _ => _) hsub, _‚ü©
    ¬∑ exact (set_pi_mem_nhds_iff hs _).mpr fun i _ => hn'' i
    ¬∑ exact forall‚ÇÇ_imp fun i _ hi' => hsub' i hi'
    ¬∑ rw [‚Üê Set.univ_pi_ite]
      refine' isCompact_univ_pi fun i => _
      by_cases h : i ‚àà s
      ¬∑ rw [if_pos h]
        exact hc i
      ¬∑ rw [if_neg h]
        exact CompactSpace.isCompact_univ‚ü©
#align pi.locally_compact_space Pi.locallyCompactSpace

instance Function.locallyCompactSpace_of_finite [Finite Œπ] [LocallyCompactSpace Œ≤] :
    LocallyCompactSpace (Œπ ‚Üí Œ≤) :=
  Pi.locallyCompactSpace_of_finite
#align function.locally_compact_space_of_finite Function.locallyCompactSpace_of_finite

instance Function.locallyCompactSpace [LocallyCompactSpace Œ≤] [CompactSpace Œ≤] :
    LocallyCompactSpace (Œπ ‚Üí Œ≤) :=
  Pi.locallyCompactSpace
#align function.locally_compact_space Function.locallyCompactSpace

end Pi

/-- A reformulation of the definition of locally compact space: In a locally compact space,
  every open set containing `x` has a compact subset containing `x` in its interior. -/
theorem exists_compact_subset [LocallyCompactSpace Œ±] {x : Œ±} {U : Set Œ±} (hU : IsOpen U)
    (hx : x ‚àà U) : ‚àÉ K : Set Œ±, IsCompact K ‚àß x ‚àà interior K ‚àß K ‚äÜ U := by
  rcases LocallyCompactSpace.local_compact_nhds x U (hU.mem_nhds hx) with ‚ü®K, h1K, h2K, h3K‚ü©
  exact ‚ü®K, h3K, mem_interior_iff_mem_nhds.2 h1K, h2K‚ü©
#align exists_compact_subset exists_compact_subset

/-- In a locally compact space every point has a compact neighborhood. -/
theorem exists_compact_mem_nhds [LocallyCompactSpace Œ±] (x : Œ±) : ‚àÉ K, IsCompact K ‚àß K ‚àà ùìù x :=
  let ‚ü®K, hKc, hx, _‚ü© := exists_compact_subset isOpen_univ (mem_univ x)
  ‚ü®K, hKc, mem_interior_iff_mem_nhds.1 hx‚ü©
#align exists_compact_mem_nhds exists_compact_mem_nhds

/-- In a locally compact space, for every containment `K ‚äÜ U` of a compact set `K` in an open
  set `U`, there is a compact neighborhood `L` such that `K ‚äÜ L ‚äÜ U`: equivalently, there is a
  compact `L` such that `K ‚äÜ interior L` and `L ‚äÜ U`. -/
theorem exists_compact_between [hŒ± : LocallyCompactSpace Œ±] {K U : Set Œ±} (hK : IsCompact K)
    (hU : IsOpen U) (h_KU : K ‚äÜ U) : ‚àÉ L, IsCompact L ‚àß K ‚äÜ interior L ‚àß L ‚äÜ U := by
  choose V hVc hxV hKV using fun x : K => exists_compact_subset hU (h_KU x.2)
  have : K ‚äÜ ‚ãÉ x, interior (V x) := fun x hx => mem_iUnion.2 ‚ü®‚ü®x, hx‚ü©, hxV _‚ü©
  rcases hK.elim_finite_subcover _ (fun x => @isOpen_interior Œ± _ (V x)) this with ‚ü®t, ht‚ü©
  refine'
    ‚ü®_, t.isCompact_biUnion fun x _ => hVc x, fun x hx => _, Set.iUnion‚ÇÇ_subset fun i _ => hKV i‚ü©
  rcases mem_iUnion‚ÇÇ.1 (ht hx) with ‚ü®y, hyt, hy‚ü©
  exact interior_mono (subset_iUnion‚ÇÇ y hyt) hy
#align exists_compact_between exists_compact_between

/-- In a locally compact space, every compact set is contained in the interior of a compact set. -/
theorem exists_compact_superset [LocallyCompactSpace Œ±] {K : Set Œ±} (hK : IsCompact K) :
    ‚àÉ K', IsCompact K' ‚àß K ‚äÜ interior K' :=
  let ‚ü®L, hLc, hKL, _‚ü© := exists_compact_between hK isOpen_univ K.subset_univ
  ‚ü®L, hLc, hKL‚ü©
#align exists_compact_superset exists_compact_superset

protected theorem ClosedEmbedding.locallyCompactSpace [LocallyCompactSpace Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : ClosedEmbedding f) : LocallyCompactSpace Œ± :=
  haveI : ‚àÄ x : Œ±, (ùìù x).HasBasis (fun s => s ‚àà ùìù (f x) ‚àß IsCompact s) fun s => f ‚Åª¬π' s := by
    intro x
    rw [hf.toInducing.nhds_eq_comap]
    exact (compact_basis_nhds _).comap _
  locallyCompactSpace_of_hasBasis this fun x s hs => hf.isCompact_preimage hs.2
#align closed_embedding.locally_compact_space ClosedEmbedding.locallyCompactSpace

protected theorem IsClosed.locallyCompactSpace [LocallyCompactSpace Œ±] {s : Set Œ±}
    (hs : IsClosed s) : LocallyCompactSpace s :=
  (closedEmbedding_subtype_val hs).locallyCompactSpace
#align is_closed.locally_compact_space IsClosed.locallyCompactSpace

protected theorem OpenEmbedding.locallyCompactSpace [LocallyCompactSpace Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : OpenEmbedding f) : LocallyCompactSpace Œ± := by
  have : ‚àÄ x : Œ±, (ùìù x).HasBasis
      (fun s => (s ‚àà ùìù (f x) ‚àß IsCompact s) ‚àß s ‚äÜ range f) fun s => f ‚Åª¬π' s := by
    intro x
    rw [hf.toInducing.nhds_eq_comap]
    exact
      ((compact_basis_nhds _).restrict_subset <| hf.open_range.mem_nhds <| mem_range_self _).comap _
  refine' locallyCompactSpace_of_hasBasis this fun x s hs => _
  rw [‚Üê hf.toInducing.isCompact_iff, image_preimage_eq_of_subset hs.2]
  exact hs.1.2
#align open_embedding.locally_compact_space OpenEmbedding.locallyCompactSpace

protected theorem IsOpen.locallyCompactSpace [LocallyCompactSpace Œ±] {s : Set Œ±} (hs : IsOpen s) :
    LocallyCompactSpace s :=
  hs.openEmbedding_subtype_val.locallyCompactSpace
#align is_open.locally_compact_space IsOpen.locallyCompactSpace

nonrec theorem Ultrafilter.le_nhds_lim [CompactSpace Œ±] (F : Ultrafilter Œ±) : ‚ÜëF ‚â§ ùìù F.lim := by
  rcases isCompact_univ.ultrafilter_le_nhds F (by simp) with ‚ü®x, -, h‚ü©
  exact le_nhds_lim ‚ü®x, h‚ü©
set_option linter.uppercaseLean3 false in
#align ultrafilter.le_nhds_Lim Ultrafilter.le_nhds_lim

theorem IsClosed.exists_minimal_nonempty_closed_subset [CompactSpace Œ±] {S : Set Œ±}
    (hS : IsClosed S) (hne : S.Nonempty) :
    ‚àÉ V : Set Œ±, V ‚äÜ S ‚àß V.Nonempty ‚àß IsClosed V ‚àß
      ‚àÄ V' : Set Œ±, V' ‚äÜ V ‚Üí V'.Nonempty ‚Üí IsClosed V' ‚Üí V' = V := by
  let opens := { U : Set Œ± | S·∂ú ‚äÜ U ‚àß IsOpen U ‚àß U·∂ú.Nonempty }
  obtain ‚ü®U, ‚ü®Uc, Uo, Ucne‚ü©, h‚ü© :=
    zorn_subset opens fun c hc hz => by
      by_cases hcne : c.Nonempty
      ¬∑ obtain ‚ü®U‚ÇÄ, hU‚ÇÄ‚ü© := hcne
        haveI : Nonempty { U // U ‚àà c } := ‚ü®‚ü®U‚ÇÄ, hU‚ÇÄ‚ü©‚ü©
        obtain ‚ü®U‚ÇÄcompl, -, -‚ü© := hc hU‚ÇÄ
        use ‚ãÉ‚ÇÄ c
        refine' ‚ü®‚ü®_, _, _‚ü©, fun U hU a ha => ‚ü®U, hU, ha‚ü©‚ü©
        ¬∑ exact fun a ha => ‚ü®U‚ÇÄ, hU‚ÇÄ, U‚ÇÄcompl ha‚ü©
        ¬∑ exact isOpen_sUnion fun _ h => (hc h).2.1
        ¬∑ convert_to (‚ãÇ U : { U // U ‚àà c }, U.1·∂ú).Nonempty
          ¬∑ ext
            simp only [not_exists, exists_prop, not_and, Set.mem_iInter, Subtype.forall,
              mem_setOf_eq, mem_compl_iff, mem_sUnion]
          apply IsCompact.nonempty_iInter_of_directed_nonempty_compact_closed
          ¬∑ rintro ‚ü®U, hU‚ü© ‚ü®U', hU'‚ü©
            obtain ‚ü®V, hVc, hVU, hVU'‚ü© := hz.directedOn U hU U' hU'
            exact ‚ü®‚ü®V, hVc‚ü©, Set.compl_subset_compl.mpr hVU, Set.compl_subset_compl.mpr hVU'‚ü©
          ¬∑ exact fun U => (hc U.2).2.2
          ¬∑ exact fun U => (hc U.2).2.1.isClosed_compl.isCompact
          ¬∑ exact fun U => (hc U.2).2.1.isClosed_compl
      ¬∑ use S·∂ú
        refine' ‚ü®‚ü®Set.Subset.refl _, isOpen_compl_iff.mpr hS, _‚ü©, fun U Uc => (hcne ‚ü®U, Uc‚ü©).elim‚ü©
        rw [compl_compl]
        exact hne
  refine' ‚ü®U·∂ú, Set.compl_subset_comm.mp Uc, Ucne, Uo.isClosed_compl, _‚ü©
  intro V' V'sub V'ne V'cls
  have : V'·∂ú = U := by
    refine' h V'·∂ú ‚ü®_, isOpen_compl_iff.mpr V'cls, _‚ü© (Set.subset_compl_comm.mp V'sub)
    exact Set.Subset.trans Uc (Set.subset_compl_comm.mp V'sub)
    simp only [compl_compl, V'ne]
  rw [‚Üê this, compl_compl]
#align is_closed.exists_minimal_nonempty_closed_subset IsClosed.exists_minimal_nonempty_closed_subset

/-- A œÉ-compact space is a space that is the union of a countable collection of compact subspaces.
  Note that a locally compact separable T‚ÇÇ space need not be œÉ-compact.
  The sequence can be extracted using `topological_space.compact_covering`. -/
class SigmaCompactSpace (Œ± : Type*) [TopologicalSpace Œ±] : Prop where
  /-- In a œÉ-compact space, there exists (by definition) a countable collection of compact subspaces
  that cover the entire space. -/
  exists_compact_covering : ‚àÉ K : ‚Ñï ‚Üí Set Œ±, (‚àÄ n, IsCompact (K n)) ‚àß ‚ãÉ n, K n = univ
#align sigma_compact_space SigmaCompactSpace

-- see Note [lower instance priority]
instance (priority := 200) CompactSpace.sigma_compact [CompactSpace Œ±] : SigmaCompactSpace Œ± :=
  ‚ü®‚ü®fun _ => univ, fun _ => isCompact_univ, iUnion_const _‚ü©‚ü©
#align compact_space.sigma_compact CompactSpace.sigma_compact

theorem SigmaCompactSpace.of_countable (S : Set (Set Œ±)) (Hc : S.Countable)
    (Hcomp : ‚àÄ s ‚àà S, IsCompact s) (HU : ‚ãÉ‚ÇÄ S = univ) : SigmaCompactSpace Œ± :=
  ‚ü®(exists_seq_cover_iff_countable ‚ü®_, isCompact_empty‚ü©).2 ‚ü®S, Hc, Hcomp, HU‚ü©‚ü©
#align sigma_compact_space.of_countable SigmaCompactSpace.of_countable

-- see Note [lower instance priority]
instance (priority := 100) sigmaCompactSpace_of_locally_compact_second_countable
    [LocallyCompactSpace Œ±] [SecondCountableTopology Œ±] : SigmaCompactSpace Œ± := by
  choose K hKc hxK using fun x : Œ± => exists_compact_mem_nhds x
  rcases countable_cover_nhds hxK with ‚ü®s, hsc, hsU‚ü©
  refine' SigmaCompactSpace.of_countable _ (hsc.image K) (ball_image_iff.2 fun x _ => hKc x) _
  rwa [sUnion_image]
#align sigma_compact_space_of_locally_compact_second_countable sigmaCompactSpace_of_locally_compact_second_countable

-- porting note: doesn't work on the same line
variable (Œ±)
variable [SigmaCompactSpace Œ±]

open SigmaCompactSpace

/-- A choice of compact covering for a `œÉ`-compact space, chosen to be monotone. -/
def compactCovering : ‚Ñï ‚Üí Set Œ± :=
  Accumulate exists_compact_covering.choose
#align compact_covering compactCovering

theorem isCompact_compactCovering (n : ‚Ñï) : IsCompact (compactCovering Œ± n) :=
  isCompact_accumulate (Classical.choose_spec SigmaCompactSpace.exists_compact_covering).1 n
#align is_compact_compact_covering isCompact_compactCovering

theorem iUnion_compactCovering : ‚ãÉ n, compactCovering Œ± n = univ := by
  rw [compactCovering, iUnion_accumulate]
  exact (Classical.choose_spec SigmaCompactSpace.exists_compact_covering).2
#align Union_compact_covering iUnion_compactCovering

@[mono]
theorem compactCovering_subset ‚¶Ém n : ‚Ñï‚¶Ñ (h : m ‚â§ n) : compactCovering Œ± m ‚äÜ compactCovering Œ± n :=
  monotone_accumulate h
#align compact_covering_subset compactCovering_subset

variable {Œ±}

theorem exists_mem_compactCovering (x : Œ±) : ‚àÉ n, x ‚àà compactCovering Œ± n :=
  iUnion_eq_univ_iff.mp (iUnion_compactCovering Œ±) x
#align exists_mem_compact_covering exists_mem_compactCovering

instance [SigmaCompactSpace Œ≤] : SigmaCompactSpace (Œ± √ó Œ≤) :=
  ‚ü®‚ü®fun n => compactCovering Œ± n √óÀ¢ compactCovering Œ≤ n, fun _ =>
      (isCompact_compactCovering _ _).prod (isCompact_compactCovering _ _), by
      simp only [iUnion_prod_of_monotone (compactCovering_subset Œ±) (compactCovering_subset Œ≤),
        iUnion_compactCovering, univ_prod_univ]‚ü©‚ü©

instance [Finite Œπ] [‚àÄ i, TopologicalSpace (œÄ i)] [‚àÄ i, SigmaCompactSpace (œÄ i)] :
    SigmaCompactSpace (‚àÄ i, œÄ i) := by
  refine' ‚ü®‚ü®fun n => Set.pi univ fun i => compactCovering (œÄ i) n,
    fun n => isCompact_univ_pi fun i => isCompact_compactCovering (œÄ i) _, _‚ü©‚ü©
  rw [iUnion_univ_pi_of_monotone]
  ¬∑ simp only [iUnion_compactCovering, pi_univ]
  ¬∑ exact fun i => compactCovering_subset (œÄ i)

instance [SigmaCompactSpace Œ≤] : SigmaCompactSpace (Sum Œ± Œ≤) :=
  ‚ü®‚ü®fun n => Sum.inl '' compactCovering Œ± n ‚à™ Sum.inr '' compactCovering Œ≤ n, fun n =>
      ((isCompact_compactCovering Œ± n).image continuous_inl).union
        ((isCompact_compactCovering Œ≤ n).image continuous_inr),
      by simp only [iUnion_union_distrib, ‚Üê image_iUnion, iUnion_compactCovering, image_univ,
        range_inl_union_range_inr]‚ü©‚ü©

instance [Countable Œπ] [‚àÄ i, TopologicalSpace (œÄ i)] [‚àÄ i, SigmaCompactSpace (œÄ i)] :
    SigmaCompactSpace (Œ£i, œÄ i) := by
  cases isEmpty_or_nonempty Œπ
  ¬∑ infer_instance
  ¬∑ rcases exists_surjective_nat Œπ with ‚ü®f, hf‚ü©
    refine' ‚ü®‚ü®fun n => ‚ãÉ k ‚â§ n, Sigma.mk (f k) '' compactCovering (œÄ (f k)) n, fun n => _, _‚ü©‚ü©
    ¬∑ refine' (finite_le_nat _).isCompact_biUnion fun k _ => _
      exact (isCompact_compactCovering _ _).image continuous_sigmaMk
    ¬∑ simp only [iUnion_eq_univ_iff, Sigma.forall, mem_iUnion]
      rw [hf.forall] -- porting note: `simp only` failed to use `hf.forall`
      intro k y
      rcases exists_mem_compactCovering y with ‚ü®n, hn‚ü©
      refine' ‚ü®max k n, k, le_max_left _ _, mem_image_of_mem _ _‚ü©
      exact compactCovering_subset _ (le_max_right _ _) hn

protected theorem ClosedEmbedding.sigmaCompactSpace {e : Œ≤ ‚Üí Œ±} (he : ClosedEmbedding e) :
    SigmaCompactSpace Œ≤ :=
  ‚ü®‚ü®fun n => e ‚Åª¬π' compactCovering Œ± n, fun n =>
      he.isCompact_preimage (isCompact_compactCovering _ _), by
      rw [‚Üê preimage_iUnion, iUnion_compactCovering, preimage_univ]‚ü©‚ü©
#align closed_embedding.sigma_compact_space ClosedEmbedding.sigmaCompactSpace

-- porting note: new lemma
theorem IsClosed.sigmaCompactSpace {s : Set Œ±} (hs : IsClosed s) : SigmaCompactSpace s :=
  (closedEmbedding_subtype_val hs).sigmaCompactSpace

instance [SigmaCompactSpace Œ≤] : SigmaCompactSpace (ULift.{u} Œ≤) :=
  ULift.closedEmbedding_down.sigmaCompactSpace

/-- If `Œ±` is a `œÉ`-compact space, then a locally finite family of nonempty sets of `Œ±` can have
only countably many elements, `Set.Countable` version. -/
protected theorem LocallyFinite.countable_univ {Œπ : Type*} {f : Œπ ‚Üí Set Œ±} (hf : LocallyFinite f)
    (hne : ‚àÄ i, (f i).Nonempty) : (univ : Set Œπ).Countable := by
  have := fun n => hf.finite_nonempty_inter_compact (isCompact_compactCovering Œ± n)
  refine (countable_iUnion fun n => (this n).countable).mono fun i _ => ?_
  rcases hne i with ‚ü®x, hx‚ü©
  rcases iUnion_eq_univ_iff.1 (iUnion_compactCovering Œ±) x with ‚ü®n, hn‚ü©
  exact mem_iUnion.2 ‚ü®n, x, hx, hn‚ü©
#align locally_finite.countable_univ LocallyFinite.countable_univ

/-- If `f : Œπ ‚Üí Set Œ±` is a locally finite covering of a œÉ-compact topological space by nonempty
sets, then the index type `Œπ` is encodable. -/
protected noncomputable def LocallyFinite.encodable {Œπ : Type*} {f : Œπ ‚Üí Set Œ±}
    (hf : LocallyFinite f) (hne : ‚àÄ i, (f i).Nonempty) : Encodable Œπ :=
  @Encodable.ofEquiv _ _ (hf.countable_univ hne).toEncodable (Equiv.Set.univ _).symm
#align locally_finite.encodable LocallyFinite.encodable

/-- In a topological space with sigma compact topology, if `f` is a function that sends each point
`x` of a closed set `s` to a neighborhood of `x` within `s`, then for some countable set `t ‚äÜ s`,
the neighborhoods `f x`, `x ‚àà t`, cover the whole set `s`. -/
theorem countable_cover_nhdsWithin_of_sigma_compact {f : Œ± ‚Üí Set Œ±} {s : Set Œ±} (hs : IsClosed s)
    (hf : ‚àÄ x ‚àà s, f x ‚àà ùìù[s] x) : ‚àÉ (t : _) (_ : t ‚äÜ s), t.Countable ‚àß s ‚äÜ ‚ãÉ x ‚àà t, f x := by
  simp only [nhdsWithin, mem_inf_principal] at hf
  choose t ht hsub using fun n =>
    ((isCompact_compactCovering Œ± n).inter_right hs).elim_nhds_subcover _ fun x hx => hf x hx.right
  refine'
    ‚ü®‚ãÉ n, (t n : Set Œ±), iUnion_subset fun n x hx => (ht n x hx).2,
      countable_iUnion fun n => (t n).countable_toSet, fun x hx => mem_iUnion‚ÇÇ.2 _‚ü©
  rcases exists_mem_compactCovering x with ‚ü®n, hn‚ü©
  rcases mem_iUnion‚ÇÇ.1 (hsub n ‚ü®hn, hx‚ü©) with ‚ü®y, hyt : y ‚àà t n, hyf : x ‚àà s ‚Üí x ‚àà f y‚ü©
  exact ‚ü®y, mem_iUnion.2 ‚ü®n, hyt‚ü©, hyf hx‚ü©
#align countable_cover_nhds_within_of_sigma_compact countable_cover_nhdsWithin_of_sigma_compact

/-- In a topological space with sigma compact topology, if `f` is a function that sends each
point `x` to a neighborhood of `x`, then for some countable set `s`, the neighborhoods `f x`,
`x ‚àà s`, cover the whole space. -/
theorem countable_cover_nhds_of_sigma_compact {f : Œ± ‚Üí Set Œ±} (hf : ‚àÄ x, f x ‚àà ùìù x) :
    ‚àÉ s : Set Œ±, s.Countable ‚àß ‚ãÉ x ‚àà s, f x = univ := by
  simp only [‚Üê nhdsWithin_univ] at hf
  rcases countable_cover_nhdsWithin_of_sigma_compact isClosed_univ fun x _ => hf x with
    ‚ü®s, -, hsc, hsU‚ü©
  exact ‚ü®s, hsc, univ_subset_iff.1 hsU‚ü©
#align countable_cover_nhds_of_sigma_compact countable_cover_nhds_of_sigma_compact

end Compact

/-- An [exhaustion by compact sets](https://en.wikipedia.org/wiki/Exhaustion_by_compact_sets) of a
topological space is a sequence of compact sets `K n` such that `K n ‚äÜ interior (K (n + 1))` and
`‚ãÉ n, K n = univ`.

If `X` is a locally compact sigma compact space, then `CompactExhaustion.choice X` provides
a choice of an exhaustion by compact sets. This choice is also available as
`(default : CompactExhaustion X)`. -/
structure CompactExhaustion (X : Type*) [TopologicalSpace X] where
  /-- The sequence of compact sets that form a compact exhaustion. -/
  toFun : ‚Ñï ‚Üí Set X
  /-- The sets in the compact exhaustion are in fact compact. -/
  isCompact' : ‚àÄ n, IsCompact (toFun n)
  /-- The sets in the compact exhaustion form a sequence:
    each set is contained in the interior of the next. -/
  subset_interior_succ' : ‚àÄ n, toFun n ‚äÜ interior (toFun (n + 1))
  /-- The union of all sets in a compact exhaustion equals the entire space. -/
  iUnion_eq' : ‚ãÉ n, toFun n = univ
#align compact_exhaustion CompactExhaustion

namespace CompactExhaustion

instance : @RelHomClass (CompactExhaustion Œ±) ‚Ñï (Set Œ±) LE.le HasSubset.Subset where
  coe := toFun
  coe_injective' | ‚ü®_, _, _, _‚ü©, ‚ü®_, _, _, _‚ü©, rfl => rfl
  map_rel f _ _ h := monotone_nat_of_le_succ
    (fun n ‚Ü¶ (f.subset_interior_succ' n).trans interior_subset) h

variable (K : CompactExhaustion Œ±)

@[simp]
theorem toFun_eq_coe : K.toFun = K := rfl

protected theorem isCompact (n : ‚Ñï) : IsCompact (K n) :=
  K.isCompact' n
#align compact_exhaustion.is_compact CompactExhaustion.isCompact

theorem subset_interior_succ (n : ‚Ñï) : K n ‚äÜ interior (K (n + 1)) :=
  K.subset_interior_succ' n
#align compact_exhaustion.subset_interior_succ CompactExhaustion.subset_interior_succ

@[mono]
protected theorem subset ‚¶Ém n : ‚Ñï‚¶Ñ (h : m ‚â§ n) : K m ‚äÜ K n :=
  OrderHomClass.mono K h
#align compact_exhaustion.subset CompactExhaustion.subset

theorem subset_succ (n : ‚Ñï) : K n ‚äÜ K (n + 1) := K.subset n.le_succ
#align compact_exhaustion.subset_succ CompactExhaustion.subset_succ

theorem subset_interior ‚¶Ém n : ‚Ñï‚¶Ñ (h : m < n) : K m ‚äÜ interior (K n) :=
  Subset.trans (K.subset_interior_succ m) <| interior_mono <| K.subset h
#align compact_exhaustion.subset_interior CompactExhaustion.subset_interior

theorem iUnion_eq : ‚ãÉ n, K n = univ :=
  K.iUnion_eq'
#align compact_exhaustion.Union_eq CompactExhaustion.iUnion_eq

theorem exists_mem (x : Œ±) : ‚àÉ n, x ‚àà K n :=
  iUnion_eq_univ_iff.1 K.iUnion_eq x
#align compact_exhaustion.exists_mem CompactExhaustion.exists_mem

/-- The minimal `n` such that `x ‚àà K n`. -/
protected noncomputable def find (x : Œ±) : ‚Ñï :=
  Nat.find (K.exists_mem x)
#align compact_exhaustion.find CompactExhaustion.find

theorem mem_find (x : Œ±) : x ‚àà K (K.find x) :=
  Nat.find_spec (K.exists_mem x)
#align compact_exhaustion.mem_find CompactExhaustion.mem_find

theorem mem_iff_find_le {x : Œ±} {n : ‚Ñï} : x ‚àà K n ‚Üî K.find x ‚â§ n :=
  ‚ü®fun h => Nat.find_min' (K.exists_mem x) h, fun h => K.subset h <| K.mem_find x‚ü©
#align compact_exhaustion.mem_iff_find_le CompactExhaustion.mem_iff_find_le

/-- Prepend the empty set to a compact exhaustion `K n`. -/
def shiftr : CompactExhaustion Œ± where
  toFun n := Nat.casesOn n ‚àÖ K
  isCompact' n := Nat.casesOn n isCompact_empty K.isCompact
  subset_interior_succ' n := Nat.casesOn n (empty_subset _) K.subset_interior_succ
  iUnion_eq' := iUnion_eq_univ_iff.2 fun x => ‚ü®K.find x + 1, K.mem_find x‚ü©
#align compact_exhaustion.shiftr CompactExhaustion.shiftr

@[simp]
theorem find_shiftr (x : Œ±) : K.shiftr.find x = K.find x + 1 :=
  Nat.find_comp_succ _ _ (not_mem_empty _)
#align compact_exhaustion.find_shiftr CompactExhaustion.find_shiftr

theorem mem_diff_shiftr_find (x : Œ±) : x ‚àà K.shiftr (K.find x + 1) \ K.shiftr (K.find x) :=
  ‚ü®K.mem_find _,
    mt K.shiftr.mem_iff_find_le.1 <| by simp only [find_shiftr, not_le, Nat.lt_succ_self]‚ü©
#align compact_exhaustion.mem_diff_shiftr_find CompactExhaustion.mem_diff_shiftr_find

/-- A choice of an
[exhaustion by compact sets](https://en.wikipedia.org/wiki/Exhaustion_by_compact_sets)
of a locally compact sigma compact space. -/
noncomputable def choice (X : Type*) [TopologicalSpace X] [LocallyCompactSpace X]
    [SigmaCompactSpace X] : CompactExhaustion X := by
  apply Classical.choice
  let K : ‚Ñï ‚Üí { s : Set X // IsCompact s } := fun n =>
    Nat.recOn n ‚ü®‚àÖ, isCompact_empty‚ü© fun n s =>
      ‚ü®(exists_compact_superset s.2).choose ‚à™ compactCovering X n,
        (exists_compact_superset s.2).choose_spec.1.union (isCompact_compactCovering _ _)‚ü©
  refine' ‚ü®‚ü®fun n => (K n).1, fun n => (K n).2, fun n => _, _‚ü©‚ü©
  ¬∑ exact Subset.trans (exists_compact_superset (K n).2).choose_spec.2
      (interior_mono <| subset_union_left _ _)
  ¬∑ refine' univ_subset_iff.1 (iUnion_compactCovering X ‚ñ∏ _)
    exact iUnion_mono' fun n => ‚ü®n + 1, subset_union_right _ _‚ü©
#align compact_exhaustion.choice CompactExhaustion.choice

noncomputable instance [LocallyCompactSpace Œ±] [SigmaCompactSpace Œ±] :
    Inhabited (CompactExhaustion Œ±) :=
  ‚ü®CompactExhaustion.choice Œ±‚ü©

end CompactExhaustion

section Clopen

-- porting note: definition moved to `Mathlib.Topology.Basic`

theorem isClopen_iff_frontier_eq_empty {s : Set Œ±} : IsClopen s ‚Üî frontier s = ‚àÖ := by
  rw [IsClopen, ‚Üê closure_eq_iff_isClosed, ‚Üê interior_eq_iff_isOpen, frontier, diff_eq_empty]
  refine' ‚ü®fun h => (h.2.trans h.1.symm).subset, fun h => _‚ü©
  exact ‚ü®interior_subset.antisymm (subset_closure.trans h),
    (h.trans interior_subset).antisymm subset_closure‚ü©
#align is_clopen_iff_frontier_eq_empty isClopen_iff_frontier_eq_empty

alias ‚ü®IsClopen.frontier_eq, _‚ü© := isClopen_iff_frontier_eq_empty
#align is_clopen.frontier_eq IsClopen.frontier_eq

theorem IsClopen.union {s t : Set Œ±} (hs : IsClopen s) (ht : IsClopen t) : IsClopen (s ‚à™ t) :=
  ‚ü®hs.1.union ht.1, hs.2.union ht.2‚ü©
#align is_clopen.union IsClopen.union

theorem IsClopen.inter {s t : Set Œ±} (hs : IsClopen s) (ht : IsClopen t) : IsClopen (s ‚à© t) :=
  ‚ü®hs.1.inter ht.1, hs.2.inter ht.2‚ü©
#align is_clopen.inter IsClopen.inter

@[simp] theorem isClopen_empty : IsClopen (‚àÖ : Set Œ±) := ‚ü®isOpen_empty, isClosed_empty‚ü©
#align is_clopen_empty isClopen_empty

@[simp] theorem isClopen_univ : IsClopen (univ : Set Œ±) := ‚ü®isOpen_univ, isClosed_univ‚ü©
#align is_clopen_univ isClopen_univ

theorem IsClopen.compl {s : Set Œ±} (hs : IsClopen s) : IsClopen s·∂ú :=
  ‚ü®hs.2.isOpen_compl, hs.1.isClosed_compl‚ü©
#align is_clopen.compl IsClopen.compl

@[simp]
theorem isClopen_compl_iff {s : Set Œ±} : IsClopen s·∂ú ‚Üî IsClopen s :=
  ‚ü®fun h => compl_compl s ‚ñ∏ IsClopen.compl h, IsClopen.compl‚ü©
#align is_clopen_compl_iff isClopen_compl_iff

theorem IsClopen.diff {s t : Set Œ±} (hs : IsClopen s) (ht : IsClopen t) : IsClopen (s \ t) :=
  hs.inter ht.compl
#align is_clopen.diff IsClopen.diff

theorem IsClopen.prod {s : Set Œ±} {t : Set Œ≤} (hs : IsClopen s) (ht : IsClopen t) :
    IsClopen (s √óÀ¢ t) :=
  ‚ü®hs.1.prod ht.1, hs.2.prod ht.2‚ü©
#align is_clopen.prod IsClopen.prod

theorem isClopen_iUnion {Œ≤ : Type*} [Finite Œ≤] {s : Œ≤ ‚Üí Set Œ±} (h : ‚àÄ i, IsClopen (s i)) :
    IsClopen (‚ãÉ i, s i) :=
  ‚ü®isOpen_iUnion (forall_and.1 h).1, isClosed_iUnion (forall_and.1 h).2‚ü©
#align is_clopen_Union isClopen_iUnion

theorem isClopen_biUnion {Œ≤ : Type*} {s : Set Œ≤} {f : Œ≤ ‚Üí Set Œ±} (hs : s.Finite)
    (h : ‚àÄ i ‚àà s, IsClopen <| f i) : IsClopen (‚ãÉ i ‚àà s, f i) :=
  ‚ü®isOpen_biUnion fun i hi => (h i hi).1, isClosed_biUnion hs fun i hi => (h i hi).2‚ü©
#align is_clopen_bUnion isClopen_biUnion

theorem isClopen_biUnion_finset {Œ≤ : Type*} {s : Finset Œ≤} {f : Œ≤ ‚Üí Set Œ±}
    (h : ‚àÄ i ‚àà s, IsClopen <| f i) : IsClopen (‚ãÉ i ‚àà s, f i) :=
  isClopen_biUnion s.finite_toSet h
#align is_clopen_bUnion_finset isClopen_biUnion_finset

theorem isClopen_iInter {Œ≤ : Type*} [Finite Œ≤] {s : Œ≤ ‚Üí Set Œ±} (h : ‚àÄ i, IsClopen (s i)) :
    IsClopen (‚ãÇ i, s i) :=
  ‚ü®isOpen_iInter (forall_and.1 h).1, isClosed_iInter (forall_and.1 h).2‚ü©
#align is_clopen_Inter isClopen_iInter

theorem isClopen_biInter {Œ≤ : Type*} {s : Set Œ≤} (hs : s.Finite) {f : Œ≤ ‚Üí Set Œ±}
    (h : ‚àÄ i ‚àà s, IsClopen (f i)) : IsClopen (‚ãÇ i ‚àà s, f i) :=
  ‚ü®isOpen_biInter hs fun i hi => (h i hi).1, isClosed_biInter fun i hi => (h i hi).2‚ü©
#align is_clopen_bInter isClopen_biInter

theorem isClopen_biInter_finset {Œ≤ : Type*} {s : Finset Œ≤} {f : Œ≤ ‚Üí Set Œ±}
    (h : ‚àÄ i ‚àà s, IsClopen (f i)) : IsClopen (‚ãÇ i ‚àà s, f i) :=
  isClopen_biInter s.finite_toSet h
#align is_clopen_bInter_finset isClopen_biInter_finset

theorem IsClopen.preimage {s : Set Œ≤} (h : IsClopen s) {f : Œ± ‚Üí Œ≤} (hf : Continuous f) :
    IsClopen (f ‚Åª¬π' s) :=
  ‚ü®h.1.preimage hf, h.2.preimage hf‚ü©
#align is_clopen.preimage IsClopen.preimage

theorem ContinuousOn.preimage_clopen_of_clopen {f : Œ± ‚Üí Œ≤} {s : Set Œ±} {t : Set Œ≤}
    (hf : ContinuousOn f s) (hs : IsClopen s) (ht : IsClopen t) : IsClopen (s ‚à© f ‚Åª¬π' t) :=
  ‚ü®ContinuousOn.preimage_open_of_open hf hs.1 ht.1,
    ContinuousOn.preimage_closed_of_closed hf hs.2 ht.2‚ü©
#align continuous_on.preimage_clopen_of_clopen ContinuousOn.preimage_clopen_of_clopen

/-- The intersection of a disjoint covering by two open sets of a clopen set will be clopen. -/
theorem isClopen_inter_of_disjoint_cover_clopen {Z a b : Set Œ±} (h : IsClopen Z) (cover : Z ‚äÜ a ‚à™ b)
    (ha : IsOpen a) (hb : IsOpen b) (hab : Disjoint a b) : IsClopen (Z ‚à© a) := by
  refine' ‚ü®IsOpen.inter h.1 ha, _‚ü©
  have : IsClosed (Z ‚à© b·∂ú) := IsClosed.inter h.2 (isClosed_compl_iff.2 hb)
  convert this using 1
  refine' (inter_subset_inter_right Z hab.subset_compl_right).antisymm _
  rintro x ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü©
  exact ‚ü®hx‚ÇÅ, by simpa [not_mem_of_mem_compl hx‚ÇÇ] using cover hx‚ÇÅ‚ü©
#align is_clopen_inter_of_disjoint_cover_clopen isClopen_inter_of_disjoint_cover_clopen

@[simp]
theorem isClopen_discrete [DiscreteTopology Œ±] (x : Set Œ±) : IsClopen x :=
  ‚ü®isOpen_discrete _, isClosed_discrete _‚ü©
#align is_clopen_discrete isClopen_discrete

-- porting note: new lemma
theorem isClopen_range_inl : IsClopen (range (Sum.inl : Œ± ‚Üí Œ± ‚äï Œ≤)) :=
  ‚ü®isOpen_range_inl, isClosed_range_inl‚ü©

-- porting note: new lemma
theorem isClopen_range_inr : IsClopen (range (Sum.inr : Œ≤ ‚Üí Œ± ‚äï Œ≤)) :=
  ‚ü®isOpen_range_inr, isClosed_range_inr‚ü©

theorem isClopen_range_sigmaMk {Œπ : Type*} {œÉ : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (œÉ i)] {i : Œπ} :
    IsClopen (Set.range (@Sigma.mk Œπ œÉ i)) :=
  ‚ü®openEmbedding_sigmaMk.open_range, closedEmbedding_sigmaMk.closed_range‚ü©
#align clopen_range_sigma_mk isClopen_range_sigmaMk

protected theorem QuotientMap.isClopen_preimage {f : Œ± ‚Üí Œ≤} (hf : QuotientMap f) {s : Set Œ≤} :
    IsClopen (f ‚Åª¬π' s) ‚Üî IsClopen s :=
  and_congr hf.isOpen_preimage hf.isClosed_preimage
#align quotient_map.is_clopen_preimage QuotientMap.isClopen_preimage

variable {X : Type*} [TopologicalSpace X]

theorem continuous_boolIndicator_iff_clopen (U : Set X) :
    Continuous U.boolIndicator ‚Üî IsClopen U := by
  rw [continuous_to_bool, preimage_boolIndicator_true]
#align continuous_bool_indicator_iff_clopen continuous_boolIndicator_iff_clopen

theorem continuousOn_boolIndicator_iff_clopen (s U : Set X) :
    ContinuousOn U.boolIndicator s ‚Üî IsClopen (((‚Üë) : s ‚Üí X) ‚Åª¬π' U) := by
  rw [continuousOn_iff_continuous_restrict, ‚Üê continuous_boolIndicator_iff_clopen]
  rfl
#align continuous_on_indicator_iff_clopen continuousOn_boolIndicator_iff_clopen

end Clopen

section Preirreducible

/-- A preirreducible set `s` is one where there is no non-trivial pair of disjoint opens on `s`. -/
def IsPreirreducible (s : Set Œ±) : Prop :=
  ‚àÄ u v : Set Œ±, IsOpen u ‚Üí IsOpen v ‚Üí (s ‚à© u).Nonempty ‚Üí (s ‚à© v).Nonempty ‚Üí (s ‚à© (u ‚à© v)).Nonempty
#align is_preirreducible IsPreirreducible

/-- An irreducible set `s` is one that is nonempty and
where there is no non-trivial pair of disjoint opens on `s`. -/
def IsIrreducible (s : Set Œ±) : Prop :=
  s.Nonempty ‚àß IsPreirreducible s
#align is_irreducible IsIrreducible

theorem IsIrreducible.nonempty {s : Set Œ±} (h : IsIrreducible s) : s.Nonempty :=
  h.1
#align is_irreducible.nonempty IsIrreducible.nonempty

theorem IsIrreducible.isPreirreducible {s : Set Œ±} (h : IsIrreducible s) : IsPreirreducible s :=
  h.2
#align is_irreducible.is_preirreducible IsIrreducible.isPreirreducible

theorem isPreirreducible_empty : IsPreirreducible (‚àÖ : Set Œ±) := fun _ _ _ _ _ ‚ü®_, h1, _‚ü© =>
  h1.elim
#align is_preirreducible_empty isPreirreducible_empty

theorem Set.Subsingleton.isPreirreducible {s : Set Œ±} (hs : s.Subsingleton) : IsPreirreducible s :=
  fun _u _v _ _ ‚ü®_x, hxs, hxu‚ü© ‚ü®y, hys, hyv‚ü© => ‚ü®y, hys, hs hxs hys ‚ñ∏ hxu, hyv‚ü©
#align set.subsingleton.is_preirreducible Set.Subsingleton.isPreirreducible

-- porting note: new lemma
theorem isPreirreducible_singleton {x} : IsPreirreducible ({x} : Set Œ±) :=
  subsingleton_singleton.isPreirreducible

theorem isIrreducible_singleton {x} : IsIrreducible ({x} : Set Œ±) :=
  ‚ü®singleton_nonempty x, isPreirreducible_singleton‚ü©
#align is_irreducible_singleton isIrreducible_singleton

theorem isPreirreducible_iff_closure {s : Set Œ±} :
    IsPreirreducible (closure s) ‚Üî IsPreirreducible s :=
  forall‚ÇÑ_congr fun u v hu hv => by
    iterate 3 rw [closure_inter_open_nonempty_iff]
    exacts [hu.inter hv, hv, hu]
#align is_preirreducible_iff_closure isPreirreducible_iff_closure

theorem isIrreducible_iff_closure {s : Set Œ±} : IsIrreducible (closure s) ‚Üî IsIrreducible s :=
  and_congr closure_nonempty_iff isPreirreducible_iff_closure
#align is_irreducible_iff_closure isIrreducible_iff_closure

protected alias ‚ü®_, IsPreirreducible.closure‚ü© := isPreirreducible_iff_closure
#align is_preirreducible.closure IsPreirreducible.closure

protected alias ‚ü®_, IsIrreducible.closure‚ü© := isIrreducible_iff_closure
#align is_irreducible.closure IsIrreducible.closure

theorem exists_preirreducible (s : Set Œ±) (H : IsPreirreducible s) :
    ‚àÉ t : Set Œ±, IsPreirreducible t ‚àß s ‚äÜ t ‚àß ‚àÄ u, IsPreirreducible u ‚Üí t ‚äÜ u ‚Üí u = t :=
  let ‚ü®m, hm, hsm, hmm‚ü© :=
    zorn_subset_nonempty { t : Set Œ± | IsPreirreducible t }
      (fun c hc hcc _ =>
        ‚ü®‚ãÉ‚ÇÄ c, fun u v hu hv ‚ü®y, hy, hyu‚ü© ‚ü®z, hz, hzv‚ü© =>
          let ‚ü®p, hpc, hyp‚ü© := mem_sUnion.1 hy
          let ‚ü®q, hqc, hzq‚ü© := mem_sUnion.1 hz
          Or.casesOn (hcc.total hpc hqc)
            (fun hpq : p ‚äÜ q =>
              let ‚ü®x, hxp, hxuv‚ü© := hc hqc u v hu hv ‚ü®y, hpq hyp, hyu‚ü© ‚ü®z, hzq, hzv‚ü©
              ‚ü®x, mem_sUnion_of_mem hxp hqc, hxuv‚ü©)
            fun hqp : q ‚äÜ p =>
            let ‚ü®x, hxp, hxuv‚ü© := hc hpc u v hu hv ‚ü®y, hyp, hyu‚ü© ‚ü®z, hqp hzq, hzv‚ü©
            ‚ü®x, mem_sUnion_of_mem hxp hpc, hxuv‚ü©,
          fun _ hxc => subset_sUnion_of_mem hxc‚ü©)
      s H
  ‚ü®m, hm, hsm, fun _u hu hmu => hmm _ hu hmu‚ü©
#align exists_preirreducible exists_preirreducible

/-- The set of irreducible components of a topological space. -/
def irreducibleComponents (Œ± : Type*) [TopologicalSpace Œ±] : Set (Set Œ±) :=
  maximals (¬∑ ‚â§ ¬∑) { s : Set Œ± | IsIrreducible s }
#align irreducible_components irreducibleComponents

theorem isClosed_of_mem_irreducibleComponents (s) (H : s ‚àà irreducibleComponents Œ±) :
    IsClosed s := by
  rw [‚Üê closure_eq_iff_isClosed, eq_comm]
  exact subset_closure.antisymm (H.2 H.1.closure subset_closure)
#align is_closed_of_mem_irreducible_components isClosed_of_mem_irreducibleComponents

theorem irreducibleComponents_eq_maximals_closed (Œ± : Type*) [TopologicalSpace Œ±] :
    irreducibleComponents Œ± = maximals (¬∑ ‚â§ ¬∑) { s : Set Œ± | IsClosed s ‚àß IsIrreducible s } := by
  ext s
  constructor
  ¬∑ intro H
    exact ‚ü®‚ü®isClosed_of_mem_irreducibleComponents _ H, H.1‚ü©, fun x h e => H.2 h.2 e‚ü©
  ¬∑ intro H
    refine' ‚ü®H.1.2, fun x h e => _‚ü©
    have : closure x ‚â§ s := H.2 ‚ü®isClosed_closure, h.closure‚ü© (e.trans subset_closure)
    exact le_trans subset_closure this
#align irreducible_components_eq_maximals_closed irreducibleComponents_eq_maximals_closed

/-- A maximal irreducible set that contains a given point. -/
def irreducibleComponent (x : Œ±) : Set Œ± :=
  Classical.choose (exists_preirreducible {x} isPreirreducible_singleton)
#align irreducible_component irreducibleComponent

theorem irreducibleComponent_property (x : Œ±) :
    IsPreirreducible (irreducibleComponent x) ‚àß
      {x} ‚äÜ irreducibleComponent x ‚àß
        ‚àÄ u, IsPreirreducible u ‚Üí irreducibleComponent x ‚äÜ u ‚Üí u = irreducibleComponent x :=
  Classical.choose_spec (exists_preirreducible {x} isPreirreducible_singleton)
#align irreducible_component_property irreducibleComponent_property

theorem mem_irreducibleComponent {x : Œ±} : x ‚àà irreducibleComponent x :=
  singleton_subset_iff.1 (irreducibleComponent_property x).2.1
#align mem_irreducible_component mem_irreducibleComponent

theorem isIrreducible_irreducibleComponent {x : Œ±} : IsIrreducible (irreducibleComponent x) :=
  ‚ü®‚ü®x, mem_irreducibleComponent‚ü©, (irreducibleComponent_property x).1‚ü©
#align is_irreducible_irreducible_component isIrreducible_irreducibleComponent

theorem eq_irreducibleComponent {x : Œ±} {s : Set Œ±} :
    IsPreirreducible s ‚Üí irreducibleComponent x ‚äÜ s ‚Üí s = irreducibleComponent x :=
  (irreducibleComponent_property x).2.2 _
#align eq_irreducible_component eq_irreducibleComponent

theorem irreducibleComponent_mem_irreducibleComponents (x : Œ±) :
    irreducibleComponent x ‚àà irreducibleComponents Œ± :=
  ‚ü®isIrreducible_irreducibleComponent, fun _ h‚ÇÅ h‚ÇÇ => (eq_irreducibleComponent h‚ÇÅ.2 h‚ÇÇ).le‚ü©
#align irreducible_component_mem_irreducible_components irreducibleComponent_mem_irreducibleComponents

theorem isClosed_irreducibleComponent {x : Œ±} : IsClosed (irreducibleComponent x) :=
  isClosed_of_mem_irreducibleComponents _ (irreducibleComponent_mem_irreducibleComponents x)
#align is_closed_irreducible_component isClosed_irreducibleComponent

/-- A preirreducible space is one where there is no non-trivial pair of disjoint opens. -/
class PreirreducibleSpace (Œ± : Type u) [TopologicalSpace Œ±] : Prop where
  /-- In a preirreducible space, `Set.univ` is a preirreducible set. -/
  isPreirreducible_univ : IsPreirreducible (univ : Set Œ±)
#align preirreducible_space PreirreducibleSpace

/-- An irreducible space is one that is nonempty
and where there is no non-trivial pair of disjoint opens. -/
class IrreducibleSpace (Œ± : Type u) [TopologicalSpace Œ±] extends PreirreducibleSpace Œ± : Prop where
  toNonempty : Nonempty Œ±
#align irreducible_space IrreducibleSpace

-- see Note [lower instance priority]
attribute [instance 50] IrreducibleSpace.toNonempty

theorem IrreducibleSpace.isIrreducible_univ (Œ± : Type u) [TopologicalSpace Œ±] [IrreducibleSpace Œ±] :
    IsIrreducible (univ : Set Œ±) :=
  ‚ü®univ_nonempty, PreirreducibleSpace.isPreirreducible_univ‚ü©
#align irreducible_space.is_irreducible_univ IrreducibleSpace.isIrreducible_univ

theorem irreducibleSpace_def (Œ± : Type u) [TopologicalSpace Œ±] :
    IrreducibleSpace Œ± ‚Üî IsIrreducible (‚ä§ : Set Œ±) :=
  ‚ü®@IrreducibleSpace.isIrreducible_univ Œ± _, fun h =>
    haveI : PreirreducibleSpace Œ± := ‚ü®h.2‚ü©
    ‚ü®‚ü®h.1.some‚ü©‚ü©‚ü©
#align irreducible_space_def irreducibleSpace_def

theorem nonempty_preirreducible_inter [PreirreducibleSpace Œ±] {s t : Set Œ±} :
    IsOpen s ‚Üí IsOpen t ‚Üí s.Nonempty ‚Üí t.Nonempty ‚Üí (s ‚à© t).Nonempty := by
  simpa only [univ_inter, univ_subset_iff] using
    @PreirreducibleSpace.isPreirreducible_univ Œ± _ _ s t
#align nonempty_preirreducible_inter nonempty_preirreducible_inter

/-- In a (pre)irreducible space, a nonempty open set is dense. -/
protected theorem IsOpen.dense [PreirreducibleSpace Œ±] {s : Set Œ±} (ho : IsOpen s)
    (hne : s.Nonempty) : Dense s :=
  dense_iff_inter_open.2 fun _t hto htne => nonempty_preirreducible_inter hto ho htne hne
#align is_open.dense IsOpen.dense

theorem IsPreirreducible.image {s : Set Œ±} (H : IsPreirreducible s) (f : Œ± ‚Üí Œ≤)
    (hf : ContinuousOn f s) : IsPreirreducible (f '' s) := by
  rintro u v hu hv ‚ü®_, ‚ü®‚ü®x, hx, rfl‚ü©, hxu‚ü©‚ü© ‚ü®_, ‚ü®‚ü®y, hy, rfl‚ü©, hyv‚ü©‚ü©
  rw [‚Üê mem_preimage] at hxu hyv
  rcases continuousOn_iff'.1 hf u hu with ‚ü®u', hu', u'_eq‚ü©
  rcases continuousOn_iff'.1 hf v hv with ‚ü®v', hv', v'_eq‚ü©
  have := H u' v' hu' hv'
  rw [inter_comm s u', ‚Üê u'_eq] at this
  rw [inter_comm s v', ‚Üê v'_eq] at this
  rcases this ‚ü®x, hxu, hx‚ü© ‚ü®y, hyv, hy‚ü© with ‚ü®z, hzs, hzu', hzv'‚ü©
  refine' ‚ü®f z, mem_image_of_mem f hzs, _, _‚ü©
  all_goals
    rw [‚Üê mem_preimage]
    apply mem_of_mem_inter_left
    show z ‚àà _ ‚à© s
    simp [*]
#align is_preirreducible.image IsPreirreducible.image

theorem IsIrreducible.image {s : Set Œ±} (H : IsIrreducible s) (f : Œ± ‚Üí Œ≤) (hf : ContinuousOn f s) :
    IsIrreducible (f '' s) :=
  ‚ü®H.nonempty.image _, H.isPreirreducible.image f hf‚ü©
#align is_irreducible.image IsIrreducible.image

theorem Subtype.preirreducibleSpace {s : Set Œ±} (h : IsPreirreducible s) :
    PreirreducibleSpace s where
  isPreirreducible_univ := by
    rintro _ _ ‚ü®u, hu, rfl‚ü© ‚ü®v, hv, rfl‚ü© ‚ü®‚ü®x, hxs‚ü©, -, hxu‚ü© ‚ü®‚ü®y, hys‚ü©, -, hyv‚ü©
    rcases h u v hu hv ‚ü®x, hxs, hxu‚ü© ‚ü®y, hys, hyv‚ü© with ‚ü®z, hzs, ‚ü®hzu, hzv‚ü©‚ü©
    exact ‚ü®‚ü®z, hzs‚ü©, ‚ü®Set.mem_univ _, ‚ü®hzu, hzv‚ü©‚ü©‚ü©
#align subtype.preirreducible_space Subtype.preirreducibleSpace

theorem Subtype.irreducibleSpace {s : Set Œ±} (h : IsIrreducible s) : IrreducibleSpace s where
  isPreirreducible_univ :=
    (Subtype.preirreducibleSpace h.isPreirreducible).isPreirreducible_univ
  toNonempty := h.nonempty.to_subtype
#align subtype.irreducible_space Subtype.irreducibleSpace

/-- An infinite type with cofinite topology is an irreducible topological space. -/
instance (priority := 100) {Œ±} [Infinite Œ±] : IrreducibleSpace (CofiniteTopology Œ±) where
  isPreirreducible_univ u v := by
    haveI : Infinite (CofiniteTopology Œ±) := ‚Äπ_‚Ä∫
    simp only [CofiniteTopology.isOpen_iff, univ_inter]
    intro hu hv hu' hv'
    simpa only [compl_union, compl_compl] using ((hu hu').union (hv hv')).infinite_compl.nonempty
  toNonempty := (inferInstance : Nonempty Œ±)

/-- A set `s` is irreducible if and only if
for every finite collection of open sets all of whose members intersect `s`,
`s` also intersects the intersection of the entire collection
(i.e., there is an element of `s` contained in every member of the collection). -/
theorem isIrreducible_iff_sInter {s : Set Œ±} :
    IsIrreducible s ‚Üî
      ‚àÄ (U : Finset (Set Œ±)), (‚àÄ u ‚àà U, IsOpen u) ‚Üí (‚àÄ u ‚àà U, (s ‚à© u).Nonempty) ‚Üí
        (s ‚à© ‚ãÇ‚ÇÄ ‚ÜëU).Nonempty := by
  refine ‚ü®fun h U hu hU => ?_, fun h => ‚ü®?_, ?_‚ü©‚ü©
  ¬∑ induction U using Finset.induction_on
    case empty => simpa using h.nonempty
    case insert u U _ IH =>
      rw [Finset.coe_insert, sInter_insert]
      rw [Finset.forall_mem_insert] at hu hU
      exact h.2 _ _ hu.1 (isOpen_sInter U.finite_toSet hu.2) hU.1 (IH hu.2 hU.2)
  ¬∑ simpa using h ‚àÖ
  ¬∑ intro u v hu hv hu' hv'
    simpa [*] using h {u, v}
#align is_irreducible_iff_sInter isIrreducible_iff_sInter

/-- A set is preirreducible if and only if
for every cover by two closed sets, it is contained in one of the two covering sets. -/
theorem isPreirreducible_iff_closed_union_closed {s : Set Œ±} :
    IsPreirreducible s ‚Üî
      ‚àÄ z‚ÇÅ z‚ÇÇ : Set Œ±, IsClosed z‚ÇÅ ‚Üí IsClosed z‚ÇÇ ‚Üí s ‚äÜ z‚ÇÅ ‚à™ z‚ÇÇ ‚Üí s ‚äÜ z‚ÇÅ ‚à® s ‚äÜ z‚ÇÇ := by
  refine compl_surjective.forall.trans <| forall_congr' fun z‚ÇÅ => compl_surjective.forall.trans <|
    forall_congr' fun z‚ÇÇ => ?_
  simp only [isOpen_compl_iff, ‚Üê compl_union, inter_compl_nonempty_iff]
  refine forall‚ÇÇ_congr fun _ _ => ?_
  rw [‚Üê and_imp, ‚Üê not_or, not_imp_not]
#align is_preirreducible_iff_closed_union_closed isPreirreducible_iff_closed_union_closed

/-- A set is irreducible if and only if for every cover by a finite collection of closed sets, it is
contained in one of the members of the collection. -/
theorem isIrreducible_iff_sUnion_closed {s : Set Œ±} :
    IsIrreducible s ‚Üî
      ‚àÄ Z : Finset (Set Œ±), (‚àÄ z ‚àà Z, IsClosed z) ‚Üí (s ‚äÜ ‚ãÉ‚ÇÄ ‚ÜëZ) ‚Üí ‚àÉ z ‚àà Z, s ‚äÜ z := by
  simp only [isIrreducible_iff_sInter]
  refine ((@compl_involutive (Set Œ±) _).toPerm _).finsetCongr.forall_congr fun {Z} => ?_
  simp_rw [Equiv.finsetCongr_apply, Finset.forall_mem_map, Finset.mem_map, Finset.coe_map,
    sUnion_image, Equiv.coe_toEmbedding, Function.Involutive.coe_toPerm, isClosed_compl_iff,
    exists_exists_and_eq_and]
  refine forall_congr' fun _ => Iff.trans ?_ not_imp_not
  simp only [not_exists, not_and, ‚Üê compl_iInter‚ÇÇ, ‚Üê sInter_eq_biInter,
    subset_compl_iff_disjoint_right, not_disjoint_iff_nonempty_inter]
#align is_irreducible_iff_sUnion_closed isIrreducible_iff_sUnion_closed

/-- A nonempty open subset of a preirreducible subspace is dense in the subspace. -/
theorem subset_closure_inter_of_isPreirreducible_of_isOpen {S U : Set Œ±} (hS : IsPreirreducible S)
    (hU : IsOpen U) (h : (S ‚à© U).Nonempty) : S ‚äÜ closure (S ‚à© U) := by
  by_contra h'
  obtain ‚ü®x, h‚ÇÅ, h‚ÇÇ, h‚ÇÉ‚ü© :=
    hS _ (closure (S ‚à© U))·∂ú hU isClosed_closure.isOpen_compl h (inter_compl_nonempty_iff.mpr h')
  exact h‚ÇÉ (subset_closure ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©)
#align subset_closure_inter_of_is_preirreducible_of_is_open subset_closure_inter_of_isPreirreducible_of_isOpen

/-- If `‚àÖ ‚â† U ‚äÜ S ‚äÜ Z` such that `U` is open and `Z` is preirreducible, then `S` is irreducible. -/
theorem IsPreirreducible.subset_irreducible {S U Z : Set Œ±} (hZ : IsPreirreducible Z)
    (hU : U.Nonempty) (hU' : IsOpen U) (h‚ÇÅ : U ‚äÜ S) (h‚ÇÇ : S ‚äÜ Z) : IsIrreducible S := by
  obtain ‚ü®z, hz‚ü© := hU
  replace hZ : IsIrreducible Z := ‚ü®‚ü®z, h‚ÇÇ (h‚ÇÅ hz)‚ü©, hZ‚ü©
  refine' ‚ü®‚ü®z, h‚ÇÅ hz‚ü©, _‚ü©
  rintro u v hu hv ‚ü®x, hx, hx'‚ü© ‚ü®y, hy, hy'‚ü©
  obtain ‚ü®a, -, ha'‚ü© : Set.Nonempty (Z ‚à© ‚ãÇ‚ÇÄ ‚Üë({U, u, v} : Finset (Set Œ±)))
  ¬∑ refine isIrreducible_iff_sInter.mp hZ {U, u, v} ?_ ?_
    ¬∑ simp [*]
    ¬∑ intro U H
      simp only [Finset.mem_insert, Finset.mem_singleton] at H
      rcases H with (rfl | rfl | rfl)
      exacts [‚ü®z, h‚ÇÇ (h‚ÇÅ hz), hz‚ü©, ‚ü®x, h‚ÇÇ hx, hx'‚ü©, ‚ü®y, h‚ÇÇ hy, hy'‚ü©]
  replace ha' : a ‚àà U ‚àß a ‚àà u ‚àß a ‚àà v := by simpa using ha'
  exact ‚ü®a, h‚ÇÅ ha'.1, ha'.2‚ü©
#align is_preirreducible.subset_irreducible IsPreirreducible.subset_irreducible

theorem IsPreirreducible.open_subset {Z U : Set Œ±} (hZ : IsPreirreducible Z) (hU : IsOpen U)
    (hU' : U ‚äÜ Z) : IsPreirreducible U :=
  U.eq_empty_or_nonempty.elim (fun h => h.symm ‚ñ∏ isPreirreducible_empty) fun h =>
    (hZ.subset_irreducible h hU (fun _ => id) hU').2
#align is_preirreducible.open_subset IsPreirreducible.open_subset

theorem IsPreirreducible.interior {Z : Set Œ±} (hZ : IsPreirreducible Z) :
    IsPreirreducible (interior Z) :=
  hZ.open_subset isOpen_interior interior_subset
#align is_preirreducible.interior IsPreirreducible.interior

theorem IsPreirreducible.preimage {Z : Set Œ±} (hZ : IsPreirreducible Z) {f : Œ≤ ‚Üí Œ±}
    (hf : OpenEmbedding f) : IsPreirreducible (f ‚Åª¬π' Z) := by
  rintro U V hU hV ‚ü®x, hx, hx'‚ü© ‚ü®y, hy, hy'‚ü©
  obtain ‚ü®_, h‚ÇÅ, ‚ü®z, h‚ÇÇ, rfl‚ü©, ‚ü®z', h‚ÇÉ, h‚ÇÑ‚ü©‚ü© :=
    hZ _ _ (hf.isOpenMap _ hU) (hf.isOpenMap _ hV) ‚ü®f x, hx, Set.mem_image_of_mem f hx'‚ü©
      ‚ü®f y, hy, Set.mem_image_of_mem f hy'‚ü©
  cases hf.inj h‚ÇÑ
  exact ‚ü®z, h‚ÇÅ, h‚ÇÇ, h‚ÇÉ‚ü©
#align is_preirreducible.preimage IsPreirreducible.preimage

end Preirreducible
