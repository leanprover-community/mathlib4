/-
Copyright (c) 2021 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤, Moritz Doll
-/
import Mathlib.Topology.Algebra.Module.LinearMap
import Mathlib.LinearAlgebra.SesquilinearForm.Basic
import Mathlib.Topology.Algebra.Module.LinearSpan
import Mathlib.Topology.Algebra.Module.StrongTopology

/-!
# Weak dual topology

This file defines the weak topology given two vector spaces `E` and `F` over a commutative semiring
`ğ•œ` and a bilinear form `B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ`. The weak topology on `E` is the coarsest topology
such that for all `y : F` every map `fun x => B x y` is continuous.

## Main definitions

The main definition is the type `WeakBilin B`.

* Given `B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ`, the type `WeakBilin B` is a type synonym for `E`.
* The instance `WeakBilin.instTopologicalSpace` is the weak topology induced by the bilinear form
  `B`.
* `LinearMap.rightDualEquiv`: When `B` is right-separating, `F` is linearly equivalent to the
  strong dual of `E` with the weak topology.
* `LinearMap.leftDualEquiv`: When `B` is left-separating, `E` is linearly equivalent to the
  strong dual of `F` with the weak topology.

## Main results

We establish that `WeakBilin B` has the following structure:
* `WeakBilin.instContinuousAdd`: The addition in `WeakBilin B` is continuous.
* `WeakBilin.instContinuousSMul`: The scalar multiplication in `WeakBilin B` is continuous.

We prove the following results characterizing the weak topology:
* `eval_continuous`: For any `y : F`, the evaluation mapping `fun x => B x y` is continuous.
* `continuous_of_continuous_eval`: For a mapping to `WeakBilin B` to be continuous,
  it suffices that its compositions with pairing with `B` at all points `y : F` is continuous.
* `tendsto_iff_forall_eval_tendsto`: Convergence in `WeakBilin B` can be characterized
  in terms of convergence of the evaluations at all points `y : F`.

## References

* [H. H. Schaefer, *Topological Vector Spaces*][schaefer1966]

## Tags

weak-star, weak dual, duality

-/


noncomputable section

open Filter

open Topology

variable {Î± ğ•œ ğ• E F : Type*}

section WeakTopology

/-- The space `E` equipped with the weak topology induced by the bilinear form `B`. -/
@[nolint unusedArguments]
def WeakBilin [CommSemiring ğ•œ] [AddCommMonoid E] [Module ğ•œ E] [AddCommMonoid F] [Module ğ•œ F]
    (_ : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) := E
deriving AddCommMonoid, Module ğ•œ

namespace WeakBilin

instance instAddCommGroup [CommSemiring ğ•œ] [a : AddCommGroup E] [Module ğ•œ E] [AddCommMonoid F]
    [Module ğ•œ F] (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) : AddCommGroup (WeakBilin B) := a

instance (priority := 100) instModule' [CommSemiring ğ•œ] [CommSemiring ğ•] [AddCommMonoid E]
    [Module ğ•œ E] [AddCommMonoid F] [Module ğ•œ F] [m : Module ğ• E] (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) :
    Module ğ• (WeakBilin B) := m

instance instIsScalarTower [CommSemiring ğ•œ] [CommSemiring ğ•] [AddCommMonoid E] [Module ğ•œ E]
    [AddCommMonoid F] [Module ğ•œ F] [SMul ğ• ğ•œ] [Module ğ• E] [s : IsScalarTower ğ• ğ•œ E]
    (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) : IsScalarTower ğ• ğ•œ (WeakBilin B) := s

section Semiring

variable [TopologicalSpace ğ•œ] [CommSemiring ğ•œ]
variable [AddCommMonoid E] [Module ğ•œ E]
variable [AddCommMonoid F] [Module ğ•œ F]
variable (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ)

instance instTopologicalSpace : TopologicalSpace (WeakBilin B) :=
  TopologicalSpace.induced (fun x y => B x y) Pi.topologicalSpace

/-- The coercion `(fun x y => B x y) : E â†’ (F â†’ ğ•œ)` is continuous. -/
theorem coeFn_continuous : Continuous fun (x : WeakBilin B) y => B x y :=
  continuous_induced_dom

@[fun_prop]
theorem eval_continuous (y : F) : Continuous fun x : WeakBilin B => B x y :=
  (continuous_pi_iff.mp (coeFn_continuous B)) y

theorem continuous_of_continuous_eval [TopologicalSpace Î±] {g : Î± â†’ WeakBilin B}
    (h : âˆ€ y, Continuous fun a => B (g a) y) : Continuous g :=
  continuous_induced_rng.2 (continuous_pi_iff.mpr h)

/-- The coercion `(fun x y => B x y) : E â†’ (F â†’ ğ•œ)` is an embedding. -/
theorem isEmbedding {B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ} (hB : Function.Injective B) :
    IsEmbedding fun (x : WeakBilin B) y => B x y :=
  Function.Injective.isEmbedding_induced <| LinearMap.coe_injective.comp hB

theorem tendsto_iff_forall_eval_tendsto {l : Filter Î±} {f : Î± â†’ WeakBilin B} {x : WeakBilin B}
    (hB : Function.Injective B) :
    Tendsto f l (ğ“ x) â†” âˆ€ y, Tendsto (fun i => B (f i) y) l (ğ“ (B x y)) := by
  rw [â† tendsto_pi_nhds, (isEmbedding hB).tendsto_nhds_iff]
  rfl

/-- Addition in `WeakBilin B` is continuous. -/
instance instContinuousAdd [ContinuousAdd ğ•œ] : ContinuousAdd (WeakBilin B) := by
  refine âŸ¨continuous_induced_rng.2 ?_âŸ©
  refine
    cast (congr_arg _ ?_)
      (((coeFn_continuous B).comp continuous_fst).add ((coeFn_continuous B).comp continuous_snd))
  ext
  simp only [Function.comp_apply, Pi.add_apply, map_add, LinearMap.add_apply]

/-- Scalar multiplication by `ğ•œ` on `WeakBilin B` is continuous. -/
instance instContinuousSMul [ContinuousSMul ğ•œ ğ•œ] : ContinuousSMul ğ•œ (WeakBilin B) := by
  refine âŸ¨continuous_induced_rng.2 ?_âŸ©
  refine cast (congr_arg _ ?_) (continuous_fst.smul ((coeFn_continuous B).comp continuous_snd))
  ext
  simp only [Function.comp_apply, Pi.smul_apply, LinearMap.map_smulâ‚›â‚—, RingHom.id_apply,
    LinearMap.smul_apply]

variable [ContinuousAdd ğ•œ] [ContinuousConstSMul ğ•œ ğ•œ]

/--
Map `F` into the topological dual of `E` with the weak topology induced by `F`
-/
def eval : F â†’â‚—[ğ•œ] StrongDual ğ•œ (WeakBilin B) where
  toFun f := âŸ¨B.flip f, by fun_propâŸ©
  map_add' _ _ := by ext; simp
  map_smul' _ _ := by ext; simp

open LinearMap in
lemma dualEmbedding_injective_of_separatingRight {E F : Type*} [AddCommGroup E] [AddCommGroup F]
    [Module ğ•œ E] [Module ğ•œ F] (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) (hr : B.SeparatingRight) :
    Function.Injective (WeakBilin.eval B) :=
  (injective_iff_map_eq_zero _).mpr (fun f hf â†¦
    (separatingRight_iff_linear_flip_nontrivial.mp hr) f (ContinuousLinearMap.coe_inj.mpr hf))

end Semiring

section Ring

variable [TopologicalSpace ğ•œ] [CommRing ğ•œ]
variable [AddCommGroup E] [Module ğ•œ E]
variable [AddCommGroup F] [Module ğ•œ F]


variable (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ)

/-- `WeakBilin B` is a `IsTopologicalAddGroup`, meaning that addition and negation are
continuous. -/
instance instIsTopologicalAddGroup [ContinuousAdd ğ•œ] : IsTopologicalAddGroup (WeakBilin B) where
  toContinuousAdd := by infer_instance
  continuous_neg := by
    refine continuous_induced_rng.2 (continuous_pi_iff.mpr fun y => ?_)
    refine cast (congr_arg _ ?_) (eval_continuous B (-y))
    ext x
    simp only [map_neg, Function.comp_apply, LinearMap.neg_apply]

end Ring

end WeakBilin

end WeakTopology

section NontriviallyNormedField

variable [NontriviallyNormedField ğ•œ] [AddCommGroup E] [AddCommGroup F]
variable [Module ğ•œ E] [Module ğ•œ F]

variable (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ)

namespace LinearMap

lemma dualEmbedding_surjective : Function.Surjective (WeakBilin.eval B) := by
  rintro âŸ¨fâ‚, hfâ‚âŸ©
  have mem_span :
    fâ‚ âˆˆ Submodule.span ğ•œ (â‡‘(WeakBilin.eval B).CLMtoLinearMapâ‚‚ '' Set.univ) := by
      rw [Set.image_univ, mem_span_iff_continuous _]
      convert hfâ‚
      simpa [WeakBilin.instTopologicalSpace] using Eq.symm (induced_to_pi ..)
  obtain âŸ¨l, _, hl2âŸ© := (Finsupp.mem_span_image_iff_linearCombination _).mp mem_span
  use Finsupp.linearCombination ğ•œ (id (M := F) (R := ğ•œ)) l
  rw [â† ContinuousLinearMap.coe_inj, WeakBilin.eval, coe_mk, AddHom.coe_mk]
  simpa [Finsupp.linearCombination_apply, map_finsuppSum, â† hl2] using (by rfl)

/-- When `B` is right-separating, `F` is linearly equivalent to the strong dual of `E` with the
weak topology. -/
noncomputable def rightDualEquiv (hr : B.SeparatingRight) : F â‰ƒâ‚—[ğ•œ] StrongDual ğ•œ (WeakBilin B) :=
  LinearEquiv.ofBijective (WeakBilin.eval B)
    âŸ¨WeakBilin.dualEmbedding_injective_of_separatingRight B hr, dualEmbedding_surjective BâŸ©

/-- When `B` is left-separating, `E` is linearly equivalent to the strong dual of `F` with the
weak topology. -/
noncomputable def leftDualEquiv (hl : B.SeparatingLeft) : E â‰ƒâ‚—[ğ•œ] StrongDual ğ•œ (WeakBilin B.flip) :=
  rightDualEquiv _ (LinearMap.flip_separatingRight.mpr hl)

end LinearMap

end NontriviallyNormedField
