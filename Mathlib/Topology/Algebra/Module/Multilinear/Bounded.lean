/-
Copyright (c) 2024 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.LocallyConvex.Bounded
import Mathlib.Topology.Algebra.Module.Multilinear.Basic

/-!
# Images of (von Neumann) bounded sets under continuous multilinear maps

In this file we prove that continuous multilinear maps
send von Neumann bounded sets to von Neumann bounded sets.

We prove 2 versions of the theorem:
one assumes that the index type is nonempty,
and the other assumes that the codomain is a topological vector space.

## Implementation notes

We do not assume the index type `Î¹` to be finite.
While for a nonzero continuous multilinear map
the family `âˆ€ i, E i` has to be essentially finite
(more precisely, all but finitely many `E i` has to be trivial),
proving theorems without a `[Finite Î¹]` assumption saves us some typeclass searches here and there.
-/

open Bornology Filter Set Function
open scoped Topology

namespace Bornology.IsVonNBounded

variable {Î¹ ð•œ F : Type*} {E : Î¹ â†’ Type*} [NormedField ð•œ]
  [âˆ€ i, AddCommGroup (E i)] [âˆ€ i, Module ð•œ (E i)] [âˆ€ i, TopologicalSpace (E i)]
  [AddCommGroup F] [Module ð•œ F] [TopologicalSpace F]

/-- The image of a von Neumann bounded set under a continuous multilinear map
is von Neumann bounded.

This version does not assume that the topologies on the domain and on the codomain
agree with the vector space structure in any way
but it assumes that `Î¹` is nonempty.
-/
theorem image_multilinear' [Nonempty Î¹] {s : Set (âˆ€ i, E i)} (hs : IsVonNBounded ð•œ s)
    (f : ContinuousMultilinearMap ð•œ E F) : IsVonNBounded ð•œ (f '' s) := fun V hV â†¦ by
  classical
  if hâ‚ : âˆ€ c : ð•œ, â€–câ€– â‰¤ 1 then
    exact absorbs_iff_norm.2 âŸ¨2, fun c hc â†¦ by linarith [hâ‚ c]âŸ©
  else
    let _ : NontriviallyNormedField ð•œ := âŸ¨by simpa using hâ‚âŸ©
    obtain âŸ¨I, t, htâ‚€, hftâŸ© :
        âˆƒ (I : Finset Î¹) (t : âˆ€ i, Set (E i)), (âˆ€ i, t i âˆˆ ð“ 0) âˆ§ Set.pi I t âŠ† f â»Â¹' V := by
      have hfV : f â»Â¹' V âˆˆ ð“ 0 := (map_continuous f).tendsto' _ _ f.map_zero hV
      rwa [nhds_pi, Filter.mem_pi, exists_finite_iff_finset] at hfV
    have : âˆ€ i, âˆƒ c : ð•œ, c â‰  0 âˆ§ âˆ€ c' : ð•œ, â€–c'â€– â‰¤ â€–câ€– â†’ âˆ€ x âˆˆ s, c' â€¢ x i âˆˆ t i := fun i â†¦ by
      rw [isVonNBounded_pi_iff] at hs
      have := (hs i).tendsto_smallSets_nhds.eventually (mem_lift' (htâ‚€ i))
      rcases NormedAddCommGroup.nhds_zero_basis_norm_lt.eventually_iff.1 this with âŸ¨r, hrâ‚€, hrâŸ©
      rcases NormedField.exists_norm_lt ð•œ hrâ‚€ with âŸ¨c, hcâ‚€, hcâŸ©
      refine âŸ¨c, norm_pos_iff.1 hcâ‚€, fun c' hle x hx â†¦ ?_âŸ©
      exact hr (hle.trans_lt hc) âŸ¨_, âŸ¨x, hx, rflâŸ©, rflâŸ©
    choose c hcâ‚€ hc using this
    rw [absorbs_iff_eventually_nhds_zero (mem_of_mem_nhds hV),
      NormedAddCommGroup.nhds_zero_basis_norm_lt.eventually_iff]
    have hcâ‚€' : âˆ i âˆˆ I, c i â‰  0 := Finset.prod_ne_zero_iff.2 fun i _ â†¦ hcâ‚€ i
    refine âŸ¨â€–âˆ i âˆˆ I, c iâ€–, norm_pos_iff.2 hcâ‚€', fun a ha â†¦ mapsTo_image_iff.2 fun x hx â†¦ ?_âŸ©
    let âŸ¨iâ‚€âŸ© := â€¹Nonempty Î¹â€º
    set y := I.piecewise (fun i â†¦ c i â€¢ x i) x
    calc
      a â€¢ f x = f (update y iâ‚€ ((a / âˆ i âˆˆ I, c i) â€¢ y iâ‚€)) := by
        rw [f.map_smul, update_eq_self, f.map_piecewise_smul, div_eq_mul_inv, mul_smul,
          inv_smul_smulâ‚€ hcâ‚€']
      _ âˆˆ V := hft fun i hi â†¦ by
        rcases eq_or_ne i iâ‚€ with rfl | hne
        Â· simp_rw [update_same, y, I.piecewise_eq_of_mem _ _ hi, smul_smul]
          refine hc _ _ ?_ _ hx
          calc
            â€–(a / âˆ i âˆˆ I, c i) * c iâ€– â‰¤ (â€–âˆ i âˆˆ I, c iâ€– / â€–âˆ i âˆˆ I, c iâ€–) * â€–c iâ€– := by
              rw [norm_mul, norm_div]; gcongr; exact ha.out.le
            _ â‰¤ 1 * â€–c iâ€– := by gcongr; apply div_self_le_one
            _ = â€–c iâ€– := one_mul _
        Â· simp_rw [update_noteq hne, y, I.piecewise_eq_of_mem _ _ hi]
          exact hc _ _ le_rfl _ hx

/-- The image of a von Neumann bounded set under a continuous multilinear map
is von Neumann bounded.

This version assumes that the codomain is a topological vector space.
-/
theorem image_multilinear [ContinuousSMul ð•œ F] {s : Set (âˆ€ i, E i)} (hs : IsVonNBounded ð•œ s)
    (f : ContinuousMultilinearMap ð•œ E F) : IsVonNBounded ð•œ (f '' s) := by
  cases isEmpty_or_nonempty Î¹ with
  | inl h =>
    exact (isBounded_iff_isVonNBounded _).1 <|
      @Set.Finite.isBounded _ (vonNBornology ð•œ F) _ (s.toFinite.image _)
  | inr h => exact hs.image_multilinear' f
