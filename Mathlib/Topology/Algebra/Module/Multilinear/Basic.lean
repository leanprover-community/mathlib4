/-
Copyright (c) 2020 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Topology.Algebra.Module.LinearMapPiProd
import Mathlib.LinearAlgebra.Multilinear.Basic

/-!
# Continuous multilinear maps

We define continuous multilinear maps as maps from `(i : Œπ) ‚Üí M‚ÇÅ i` to `M‚ÇÇ` which are multilinear
and continuous, by extending the space of multilinear maps with a continuity assumption.
Here, `M‚ÇÅ i` and `M‚ÇÇ` are modules over a ring `R`, and `Œπ` is an arbitrary type, and all these
spaces are also topological spaces.

## Main definitions

* `ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ` is the space of continuous multilinear maps from
  `(i : Œπ) ‚Üí M‚ÇÅ i` to `M‚ÇÇ`. We show that it is an `R`-module.

## Implementation notes

We mostly follow the API of multilinear maps.

## Notation

We introduce the notation `M [√ón]‚ÜíL[R] M'` for the space of continuous `n`-multilinear maps from
`M^n` to `M'`. This is a particular case of the general notion (where we allow varying dependent
types as the arguments of our continuous multilinear maps), but arguably the most important one,
especially when defining iterated derivatives.
-/


open Function Fin Set

universe u v w w‚ÇÅ w‚ÇÅ' w‚ÇÇ w‚ÇÉ w‚ÇÑ

variable {R : Type u} {Œπ : Type v} {n : ‚Ñï} {M : Fin n.succ ‚Üí Type w} {M‚ÇÅ : Œπ ‚Üí Type w‚ÇÅ}
  {M‚ÇÅ' : Œπ ‚Üí Type w‚ÇÅ'} {M‚ÇÇ : Type w‚ÇÇ} {M‚ÇÉ : Type w‚ÇÉ} {M‚ÇÑ : Type w‚ÇÑ}

/-- Continuous multilinear maps over the ring `R`, from `‚àÄ i, M‚ÇÅ i` to `M‚ÇÇ` where `M‚ÇÅ i` and `M‚ÇÇ`
are modules over `R` with a topological structure. In applications, there will be compatibility
conditions between the algebraic and the topological structures, but this is not needed for the
definition. -/
structure ContinuousMultilinearMap (R : Type u) {Œπ : Type v} (M‚ÇÅ : Œπ ‚Üí Type w‚ÇÅ) (M‚ÇÇ : Type w‚ÇÇ)
  [Semiring R] [‚àÄ i, AddCommMonoid (M‚ÇÅ i)] [AddCommMonoid M‚ÇÇ] [‚àÄ i, Module R (M‚ÇÅ i)] [Module R M‚ÇÇ]
  [‚àÄ i, TopologicalSpace (M‚ÇÅ i)] [TopologicalSpace M‚ÇÇ] extends MultilinearMap R M‚ÇÅ M‚ÇÇ where
  cont : Continuous toFun

attribute [inherit_doc ContinuousMultilinearMap] ContinuousMultilinearMap.cont

@[inherit_doc]
notation:25 M " [√ó" n "]‚ÜíL[" R "] " M' => ContinuousMultilinearMap R (fun i : Fin n => M) M'

namespace ContinuousMultilinearMap

section Semiring

variable [Semiring R] [‚àÄ i, AddCommMonoid (M i)] [‚àÄ i, AddCommMonoid (M‚ÇÅ i)]
  [‚àÄ i, AddCommMonoid (M‚ÇÅ' i)] [AddCommMonoid M‚ÇÇ] [AddCommMonoid M‚ÇÉ] [AddCommMonoid M‚ÇÑ]
  [‚àÄ i, Module R (M i)] [‚àÄ i, Module R (M‚ÇÅ i)] [‚àÄ i, Module R (M‚ÇÅ' i)] [Module R M‚ÇÇ] [Module R M‚ÇÉ]
  [Module R M‚ÇÑ] [‚àÄ i, TopologicalSpace (M i)] [‚àÄ i, TopologicalSpace (M‚ÇÅ i)]
  [‚àÄ i, TopologicalSpace (M‚ÇÅ' i)] [TopologicalSpace M‚ÇÇ] [TopologicalSpace M‚ÇÉ] [TopologicalSpace M‚ÇÑ]
  (f f' : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ)

theorem toMultilinearMap_injective :
    Function.Injective
      (ContinuousMultilinearMap.toMultilinearMap :
        ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ ‚Üí MultilinearMap R M‚ÇÅ M‚ÇÇ)
  | ‚ü®f, hf‚ü©, ‚ü®g, hg‚ü©, h => by subst h; rfl

instance funLike : FunLike (ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ) (‚àÄ i, M‚ÇÅ i) M‚ÇÇ where
  coe f := f.toFun
  coe_injective' _ _ h := toMultilinearMap_injective <| MultilinearMap.coe_injective h

instance continuousMapClass :
    ContinuousMapClass (ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ) (‚àÄ i, M‚ÇÅ i) M‚ÇÇ where
  map_continuous := ContinuousMultilinearMap.cont

/-- See Note [custom simps projection]. We need to specify this projection explicitly in this case,
  because it is a composition of multiple projections. -/
def Simps.apply (L‚ÇÅ : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ) (v : ‚àÄ i, M‚ÇÅ i) : M‚ÇÇ :=
  L‚ÇÅ v

initialize_simps_projections ContinuousMultilinearMap (-toMultilinearMap,
  toMultilinearMap_toFun ‚Üí apply)

@[continuity]
theorem coe_continuous : Continuous (f : (‚àÄ i, M‚ÇÅ i) ‚Üí M‚ÇÇ) :=
  f.cont

@[simp]
theorem coe_coe : (f.toMultilinearMap : (‚àÄ i, M‚ÇÅ i) ‚Üí M‚ÇÇ) = f :=
  rfl

@[ext]
theorem ext {f f' : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ} (H : ‚àÄ x, f x = f' x) : f = f' :=
  DFunLike.ext _ _ H

@[simp]
theorem map_update_add [DecidableEq Œπ] (m : ‚àÄ i, M‚ÇÅ i) (i : Œπ) (x y : M‚ÇÅ i) :
    f (update m i (x + y)) = f (update m i x) + f (update m i y) :=
  f.map_update_add' m i x y

@[deprecated (since := "2024-11-03")]
protected alias map_add := ContinuousMultilinearMap.map_update_add

@[simp]
theorem map_update_smul [DecidableEq Œπ] (m : ‚àÄ i, M‚ÇÅ i) (i : Œπ) (c : R) (x : M‚ÇÅ i) :
    f (update m i (c ‚Ä¢ x)) = c ‚Ä¢ f (update m i x) :=
  f.map_update_smul' m i c x

@[deprecated (since := "2024-11-03")]
protected alias map_smul := ContinuousMultilinearMap.map_update_smul

theorem map_coord_zero {m : ‚àÄ i, M‚ÇÅ i} (i : Œπ) (h : m i = 0) : f m = 0 :=
  f.toMultilinearMap.map_coord_zero i h

@[simp]
theorem map_zero [Nonempty Œπ] : f 0 = 0 :=
  f.toMultilinearMap.map_zero

instance : Zero (ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ) :=
  ‚ü®{ (0 : MultilinearMap R M‚ÇÅ M‚ÇÇ) with cont := continuous_const }‚ü©

instance : Inhabited (ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ) :=
  ‚ü®0‚ü©

@[simp]
theorem zero_apply (m : ‚àÄ i, M‚ÇÅ i) : (0 : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ) m = 0 :=
  rfl

@[simp]
theorem toMultilinearMap_zero : (0 : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ).toMultilinearMap = 0 :=
  rfl

section SMul

variable {R' R'' A : Type*} [Monoid R'] [Monoid R''] [Semiring A] [‚àÄ i, Module A (M‚ÇÅ i)]
  [Module A M‚ÇÇ] [DistribMulAction R' M‚ÇÇ] [ContinuousConstSMul R' M‚ÇÇ] [SMulCommClass A R' M‚ÇÇ]
  [DistribMulAction R'' M‚ÇÇ] [ContinuousConstSMul R'' M‚ÇÇ] [SMulCommClass A R'' M‚ÇÇ]

instance : SMul R' (ContinuousMultilinearMap A M‚ÇÅ M‚ÇÇ) :=
  ‚ü®fun c f => { c ‚Ä¢ f.toMultilinearMap with cont := f.cont.const_smul c }‚ü©

@[simp]
theorem smul_apply (f : ContinuousMultilinearMap A M‚ÇÅ M‚ÇÇ) (c : R') (m : ‚àÄ i, M‚ÇÅ i) :
    (c ‚Ä¢ f) m = c ‚Ä¢ f m :=
  rfl

@[simp]
theorem toMultilinearMap_smul (c : R') (f : ContinuousMultilinearMap A M‚ÇÅ M‚ÇÇ) :
    (c ‚Ä¢ f).toMultilinearMap = c ‚Ä¢ f.toMultilinearMap :=
  rfl

instance [SMulCommClass R' R'' M‚ÇÇ] : SMulCommClass R' R'' (ContinuousMultilinearMap A M‚ÇÅ M‚ÇÇ) :=
  ‚ü®fun _ _ _ => ext fun _ => smul_comm _ _ _‚ü©

instance [SMul R' R''] [IsScalarTower R' R'' M‚ÇÇ] :
    IsScalarTower R' R'' (ContinuousMultilinearMap A M‚ÇÅ M‚ÇÇ) :=
  ‚ü®fun _ _ _ => ext fun _ => smul_assoc _ _ _‚ü©

instance [DistribMulAction R'·µê·µí·µñ M‚ÇÇ] [IsCentralScalar R' M‚ÇÇ] :
    IsCentralScalar R' (ContinuousMultilinearMap A M‚ÇÅ M‚ÇÇ) :=
  ‚ü®fun _ _ => ext fun _ => op_smul_eq_smul _ _‚ü©

instance : MulAction R' (ContinuousMultilinearMap A M‚ÇÅ M‚ÇÇ) :=
  Function.Injective.mulAction toMultilinearMap toMultilinearMap_injective fun _ _ => rfl

end SMul

section ContinuousAdd

variable [ContinuousAdd M‚ÇÇ]

instance : Add (ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ) :=
  ‚ü®fun f f' => ‚ü®f.toMultilinearMap + f'.toMultilinearMap, f.cont.add f'.cont‚ü©‚ü©

@[simp]
theorem add_apply (m : ‚àÄ i, M‚ÇÅ i) : (f + f') m = f m + f' m :=
  rfl

@[simp]
theorem toMultilinearMap_add (f g : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ) :
    (f + g).toMultilinearMap = f.toMultilinearMap + g.toMultilinearMap :=
  rfl

instance addCommMonoid : AddCommMonoid (ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ) :=
  toMultilinearMap_injective.addCommMonoid _ rfl (fun _ _ => rfl) fun _ _ => rfl

/-- Evaluation of a `ContinuousMultilinearMap` at a vector as an `AddMonoidHom`. -/
def applyAddHom (m : ‚àÄ i, M‚ÇÅ i) : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ ‚Üí+ M‚ÇÇ where
  toFun f := f m
  map_zero' := rfl
  map_add' _ _ := rfl

@[simp]
theorem sum_apply {Œ± : Type*} (f : Œ± ‚Üí ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ) (m : ‚àÄ i, M‚ÇÅ i)
    {s : Finset Œ±} : (‚àë a ‚àà s, f a) m = ‚àë a ‚àà s, f a m :=
  map_sum (applyAddHom m) f s

end ContinuousAdd

/-- If `f` is a continuous multilinear map, then `f.toContinuousLinearMap m i` is the continuous
linear map obtained by fixing all coordinates but `i` equal to those of `m`, and varying the
`i`-th coordinate. -/
@[simps!] def toContinuousLinearMap [DecidableEq Œπ] (m : ‚àÄ i, M‚ÇÅ i) (i : Œπ) : M‚ÇÅ i ‚ÜíL[R] M‚ÇÇ :=
  { f.toMultilinearMap.toLinearMap m i with
    cont := f.cont.comp (continuous_const.update i continuous_id) }

/-- The cartesian product of two continuous multilinear maps, as a continuous multilinear map. -/
def prod (f : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ) (g : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÉ) :
    ContinuousMultilinearMap R M‚ÇÅ (M‚ÇÇ √ó M‚ÇÉ) :=
  { f.toMultilinearMap.prod g.toMultilinearMap with cont := f.cont.prodMk g.cont }

@[simp]
theorem prod_apply (f : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ) (g : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÉ)
    (m : ‚àÄ i, M‚ÇÅ i) : (f.prod g) m = (f m, g m) :=
  rfl

/-- Combine a family of continuous multilinear maps with the same domain and codomains `M' i` into a
continuous multilinear map taking values in the space of functions `‚àÄ i, M' i`. -/
def pi {Œπ' : Type*} {M' : Œπ' ‚Üí Type*} [‚àÄ i, AddCommMonoid (M' i)] [‚àÄ i, TopologicalSpace (M' i)]
    [‚àÄ i, Module R (M' i)] (f : ‚àÄ i, ContinuousMultilinearMap R M‚ÇÅ (M' i)) :
    ContinuousMultilinearMap R M‚ÇÅ (‚àÄ i, M' i) where
  cont := continuous_pi fun i => (f i).coe_continuous
  toMultilinearMap := MultilinearMap.pi fun i => (f i).toMultilinearMap

@[simp]
theorem coe_pi {Œπ' : Type*} {M' : Œπ' ‚Üí Type*} [‚àÄ i, AddCommMonoid (M' i)]
    [‚àÄ i, TopologicalSpace (M' i)] [‚àÄ i, Module R (M' i)]
    (f : ‚àÄ i, ContinuousMultilinearMap R M‚ÇÅ (M' i)) : ‚áë(pi f) = fun m j => f j m :=
  rfl

theorem pi_apply {Œπ' : Type*} {M' : Œπ' ‚Üí Type*} [‚àÄ i, AddCommMonoid (M' i)]
    [‚àÄ i, TopologicalSpace (M' i)] [‚àÄ i, Module R (M' i)]
    (f : ‚àÄ i, ContinuousMultilinearMap R M‚ÇÅ (M' i)) (m : ‚àÄ i, M‚ÇÅ i) (j : Œπ') : pi f m j = f j m :=
  rfl

/-- Restrict the codomain of a continuous multilinear map to a submodule. -/
@[simps! toMultilinearMap apply_coe]
def codRestrict (f : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ) (p : Submodule R M‚ÇÇ) (h : ‚àÄ v, f v ‚àà p) :
    ContinuousMultilinearMap R M‚ÇÅ p :=
  ‚ü®f.1.codRestrict p h, f.cont.subtype_mk _‚ü©

section

variable (R M‚ÇÇ M‚ÇÉ)

/-- The natural equivalence between continuous linear maps from `M‚ÇÇ` to `M‚ÇÉ`
and continuous 1-multilinear maps from `M‚ÇÇ` to `M‚ÇÉ`. -/
@[simps! apply_toMultilinearMap apply_apply symm_apply_apply]
def ofSubsingleton [Subsingleton Œπ] (i : Œπ) :
    (M‚ÇÇ ‚ÜíL[R] M‚ÇÉ) ‚âÉ ContinuousMultilinearMap R (fun _ : Œπ => M‚ÇÇ) M‚ÇÉ where
  toFun f := ‚ü®MultilinearMap.ofSubsingleton R M‚ÇÇ M‚ÇÉ i f,
    (map_continuous f).comp (continuous_apply i)‚ü©
  invFun f := ‚ü®(MultilinearMap.ofSubsingleton R M‚ÇÇ M‚ÇÉ i).symm f.toMultilinearMap,
    (map_continuous f).comp <| continuous_pi fun _ ‚Ü¶ continuous_id‚ü©
  right_inv f := toMultilinearMap_injective <|
    (MultilinearMap.ofSubsingleton R M‚ÇÇ M‚ÇÉ i).apply_symm_apply f.toMultilinearMap

variable (M‚ÇÅ) {M‚ÇÇ}

/-- The constant map is multilinear when `Œπ` is empty. -/
@[simps! toMultilinearMap apply]
def constOfIsEmpty [IsEmpty Œπ] (m : M‚ÇÇ) : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ where
  toMultilinearMap := MultilinearMap.constOfIsEmpty R _ m
  cont := continuous_const

end

/-- If `g` is continuous multilinear and `f` is a collection of continuous linear maps,
then `g (f‚ÇÅ m‚ÇÅ, ..., f‚Çô m‚Çô)` is again a continuous multilinear map, that we call
`g.compContinuousLinearMap f`. -/
def compContinuousLinearMap (g : ContinuousMultilinearMap R M‚ÇÅ' M‚ÇÑ)
    (f : ‚àÄ i : Œπ, M‚ÇÅ i ‚ÜíL[R] M‚ÇÅ' i) : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÑ :=
  { g.toMultilinearMap.compLinearMap fun i => (f i).toLinearMap with
    cont := g.cont.comp <| continuous_pi fun j => (f j).cont.comp <| continuous_apply _ }

@[simp]
theorem compContinuousLinearMap_apply (g : ContinuousMultilinearMap R M‚ÇÅ' M‚ÇÑ)
    (f : ‚àÄ i : Œπ, M‚ÇÅ i ‚ÜíL[R] M‚ÇÅ' i) (m : ‚àÄ i, M‚ÇÅ i) :
    g.compContinuousLinearMap f m = g fun i => f i <| m i :=
  rfl

/-- Composing a continuous multilinear map with a continuous linear map gives again a
continuous multilinear map. -/
def _root_.ContinuousLinearMap.compContinuousMultilinearMap (g : M‚ÇÇ ‚ÜíL[R] M‚ÇÉ)
    (f : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ) : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÉ :=
  { g.toLinearMap.compMultilinearMap f.toMultilinearMap with cont := g.cont.comp f.cont }

@[simp]
theorem _root_.ContinuousLinearMap.compContinuousMultilinearMap_coe (g : M‚ÇÇ ‚ÜíL[R] M‚ÇÉ)
    (f : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ) :
    (g.compContinuousMultilinearMap f : (‚àÄ i, M‚ÇÅ i) ‚Üí M‚ÇÉ) =
      (g : M‚ÇÇ ‚Üí M‚ÇÉ) ‚àò (f : (‚àÄ i, M‚ÇÅ i) ‚Üí M‚ÇÇ) := by
  ext m
  rfl

/-- `ContinuousMultilinearMap.prod` as an `Equiv`. -/
@[simps apply symm_apply_fst symm_apply_snd, simps -isSimp symm_apply]
def prodEquiv :
    (ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ √ó ContinuousMultilinearMap R M‚ÇÅ M‚ÇÉ) ‚âÉ
      ContinuousMultilinearMap R M‚ÇÅ (M‚ÇÇ √ó M‚ÇÉ) where
  toFun f := f.1.prod f.2
  invFun f := ((ContinuousLinearMap.fst _ _ _).compContinuousMultilinearMap f,
    (ContinuousLinearMap.snd _ _ _).compContinuousMultilinearMap f)

theorem prod_ext_iff {f g : ContinuousMultilinearMap R M‚ÇÅ (M‚ÇÇ √ó M‚ÇÉ)} :
    f = g ‚Üî (ContinuousLinearMap.fst _ _ _).compContinuousMultilinearMap f =
      (ContinuousLinearMap.fst _ _ _).compContinuousMultilinearMap g ‚àß
      (ContinuousLinearMap.snd _ _ _).compContinuousMultilinearMap f =
      (ContinuousLinearMap.snd _ _ _).compContinuousMultilinearMap g := by
  rw [‚Üê Prod.mk_inj, ‚Üê prodEquiv_symm_apply, ‚Üê prodEquiv_symm_apply, Equiv.apply_eq_iff_eq]

@[ext]
theorem prod_ext {f g : ContinuousMultilinearMap R M‚ÇÅ (M‚ÇÇ √ó M‚ÇÉ)}
    (h‚ÇÅ : (ContinuousLinearMap.fst _ _ _).compContinuousMultilinearMap f =
      (ContinuousLinearMap.fst _ _ _).compContinuousMultilinearMap g)
    (h‚ÇÇ : (ContinuousLinearMap.snd _ _ _).compContinuousMultilinearMap f =
      (ContinuousLinearMap.snd _ _ _).compContinuousMultilinearMap g) : f = g :=
  prod_ext_iff.mpr ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©

theorem eq_prod_iff {f : ContinuousMultilinearMap R M‚ÇÅ (M‚ÇÇ √ó M‚ÇÉ)}
    {g : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ} {h : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÉ} :
    f = g.prod h ‚Üî (ContinuousLinearMap.fst _ _ _).compContinuousMultilinearMap f = g ‚àß
      (ContinuousLinearMap.snd _ _ _).compContinuousMultilinearMap f = h :=
  prod_ext_iff

theorem add_prod_add [ContinuousAdd M‚ÇÇ] [ContinuousAdd M‚ÇÉ]
    (f‚ÇÅ f‚ÇÇ : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ) (g‚ÇÅ g‚ÇÇ : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÉ) :
    (f‚ÇÅ + f‚ÇÇ).prod (g‚ÇÅ + g‚ÇÇ) = f‚ÇÅ.prod g‚ÇÅ + f‚ÇÇ.prod g‚ÇÇ :=
  rfl

theorem smul_prod_smul {S : Type*} [Monoid S] [DistribMulAction S M‚ÇÇ] [DistribMulAction S M‚ÇÉ]
    [ContinuousConstSMul S M‚ÇÇ] [SMulCommClass R S M‚ÇÇ]
    [ContinuousConstSMul S M‚ÇÉ] [SMulCommClass R S M‚ÇÉ]
    (c : S) (f : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ) (g : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÉ) :
    (c ‚Ä¢ f).prod (c ‚Ä¢ g) = c ‚Ä¢ f.prod g :=
  rfl

@[simp]
theorem zero_prod_zero :
    (0 : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ).prod (0 : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÉ) = 0 :=
  rfl

/-- `ContinuousMultilinearMap.pi` as an `Equiv`. -/
@[simps]
def piEquiv {Œπ' : Type*} {M' : Œπ' ‚Üí Type*} [‚àÄ i, AddCommMonoid (M' i)]
    [‚àÄ i, TopologicalSpace (M' i)] [‚àÄ i, Module R (M' i)] :
    (‚àÄ i, ContinuousMultilinearMap R M‚ÇÅ (M' i)) ‚âÉ ContinuousMultilinearMap R M‚ÇÅ (‚àÄ i, M' i) where
  toFun := ContinuousMultilinearMap.pi
  invFun f i := (ContinuousLinearMap.proj i : _ ‚ÜíL[R] M' i).compContinuousMultilinearMap f

/-- An equivalence of the index set defines an equivalence between the spaces of continuous
multilinear maps. This is the forward map of this equivalence. -/
@[simps! toMultilinearMap apply]
nonrec def domDomCongr {Œπ' : Type*} (e : Œπ ‚âÉ Œπ')
    (f : ContinuousMultilinearMap R (fun _ : Œπ => M‚ÇÇ) M‚ÇÉ) :
    ContinuousMultilinearMap R (fun _ : Œπ' => M‚ÇÇ) M‚ÇÉ where
  toMultilinearMap := f.domDomCongr e
  cont := f.cont.comp <| continuous_pi fun _ => continuous_apply _

/-- An equivalence of the index set defines an equivalence between the spaces of continuous
multilinear maps. In case of normed spaces, this is a linear isometric equivalence, see
`ContinuousMultilinearMap.domDomCongr‚Çó·µ¢`. -/
@[simps]
def domDomCongrEquiv {Œπ' : Type*} (e : Œπ ‚âÉ Œπ') :
    ContinuousMultilinearMap R (fun _ : Œπ => M‚ÇÇ) M‚ÇÉ ‚âÉ
      ContinuousMultilinearMap R (fun _ : Œπ' => M‚ÇÇ) M‚ÇÉ where
  toFun := domDomCongr e
  invFun := domDomCongr e.symm
  left_inv _ := ext fun _ => by simp
  right_inv _ := ext fun _ => by simp

section linearDeriv

variable [ContinuousAdd M‚ÇÇ] [DecidableEq Œπ] [Fintype Œπ] (x y : ‚àÄ i, M‚ÇÅ i)

/-- The derivative of a continuous multilinear map, as a continuous linear map
from `‚àÄ i, M‚ÇÅ i` to `M‚ÇÇ`; see `ContinuousMultilinearMap.hasFDerivAt`. -/
def linearDeriv : (‚àÄ i, M‚ÇÅ i) ‚ÜíL[R] M‚ÇÇ := ‚àë i : Œπ, (f.toContinuousLinearMap x i).comp (.proj i)

@[simp]
lemma linearDeriv_apply : f.linearDeriv x y = ‚àë i, f (Function.update x i (y i)) := by
  unfold linearDeriv toContinuousLinearMap
  simp only [ContinuousLinearMap.coe_sum', ContinuousLinearMap.coe_comp',
    ContinuousLinearMap.coe_mk', LinearMap.coe_mk, LinearMap.coe_toAddHom, Finset.sum_apply]
  rfl

end linearDeriv

/-- In the specific case of continuous multilinear maps on spaces indexed by `Fin (n+1)`, where one
can build an element of `(i : Fin (n+1)) ‚Üí M i` using `cons`, one can express directly the
additivity of a multilinear map along the first variable. -/
theorem cons_add (f : ContinuousMultilinearMap R M M‚ÇÇ) (m : ‚àÄ i : Fin n, M i.succ) (x y : M 0) :
    f (cons (x + y) m) = f (cons x m) + f (cons y m) :=
  f.toMultilinearMap.cons_add m x y

/-- In the specific case of continuous multilinear maps on spaces indexed by `Fin (n+1)`, where one
can build an element of `(i : Fin (n+1)) ‚Üí M i` using `cons`, one can express directly the
multiplicativity of a multilinear map along the first variable. -/
theorem cons_smul (f : ContinuousMultilinearMap R M M‚ÇÇ) (m : ‚àÄ i : Fin n, M i.succ) (c : R)
    (x : M 0) : f (cons (c ‚Ä¢ x) m) = c ‚Ä¢ f (cons x m) :=
  f.toMultilinearMap.cons_smul m c x

theorem map_piecewise_add [DecidableEq Œπ] (m m' : ‚àÄ i, M‚ÇÅ i) (t : Finset Œπ) :
    f (t.piecewise (m + m') m') = ‚àë s ‚àà t.powerset, f (s.piecewise m m') :=
  f.toMultilinearMap.map_piecewise_add _ _ _

/-- Additivity of a continuous multilinear map along all coordinates at the same time,
writing `f (m + m')` as the sum of `f (s.piecewise m m')` over all sets `s`. -/
theorem map_add_univ [DecidableEq Œπ] [Fintype Œπ] (m m' : ‚àÄ i, M‚ÇÅ i) :
    f (m + m') = ‚àë s : Finset Œπ, f (s.piecewise m m') :=
  f.toMultilinearMap.map_add_univ _ _

section ApplySum

open Fintype Finset

variable {Œ± : Œπ ‚Üí Type*} [Fintype Œπ] (g : ‚àÄ i, Œ± i ‚Üí M‚ÇÅ i) (A : ‚àÄ i, Finset (Œ± i))

/-- If `f` is continuous multilinear, then `f (Œ£_{j‚ÇÅ ‚àà A‚ÇÅ} g‚ÇÅ j‚ÇÅ, ..., Œ£_{j‚Çô ‚àà A‚Çô} g‚Çô j‚Çô)` is the
sum of `f (g‚ÇÅ (r 1), ..., g‚Çô (r n))` where `r` ranges over all functions with `r 1 ‚àà A‚ÇÅ`, ...,
`r n ‚àà A‚Çô`. This follows from multilinearity by expanding successively with respect to each
coordinate. -/
theorem map_sum_finset [DecidableEq Œπ] :
    (f fun i => ‚àë j ‚àà A i, g i j) = ‚àë r ‚àà piFinset A, f fun i => g i (r i) :=
  f.toMultilinearMap.map_sum_finset _ _

/-- If `f` is continuous multilinear, then `f (Œ£_{j‚ÇÅ} g‚ÇÅ j‚ÇÅ, ..., Œ£_{j‚Çô} g‚Çô j‚Çô)` is the sum of
`f (g‚ÇÅ (r 1), ..., g‚Çô (r n))` where `r` ranges over all functions `r`. This follows from
multilinearity by expanding successively with respect to each coordinate. -/
theorem map_sum [DecidableEq Œπ] [‚àÄ i, Fintype (Œ± i)] :
    (f fun i => ‚àë j, g i j) = ‚àë r : ‚àÄ i, Œ± i, f fun i => g i (r i) :=
  f.toMultilinearMap.map_sum _

end ApplySum

section RestrictScalar

variable (R)
variable {A : Type*} [Semiring A] [SMul R A] [‚àÄ i : Œπ, Module A (M‚ÇÅ i)] [Module A M‚ÇÇ]
  [‚àÄ i, IsScalarTower R A (M‚ÇÅ i)] [IsScalarTower R A M‚ÇÇ]

/-- Reinterpret an `A`-multilinear map as an `R`-multilinear map, if `A` is an algebra over `R`
and their actions on all involved modules agree with the action of `R` on `A`. -/
def restrictScalars (f : ContinuousMultilinearMap A M‚ÇÅ M‚ÇÇ) : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ where
  toMultilinearMap := f.toMultilinearMap.restrictScalars R
  cont := f.cont

@[simp]
theorem coe_restrictScalars (f : ContinuousMultilinearMap A M‚ÇÅ M‚ÇÇ) : ‚áë(f.restrictScalars R) = f :=
  rfl

end RestrictScalar

end Semiring

section Ring

variable [Ring R] [‚àÄ i, AddCommGroup (M‚ÇÅ i)] [AddCommGroup M‚ÇÇ] [‚àÄ i, Module R (M‚ÇÅ i)] [Module R M‚ÇÇ]
  [‚àÄ i, TopologicalSpace (M‚ÇÅ i)] [TopologicalSpace M‚ÇÇ] (f f' : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ)

@[simp]
theorem map_update_sub [DecidableEq Œπ] (m : ‚àÄ i, M‚ÇÅ i) (i : Œπ) (x y : M‚ÇÅ i) :
    f (update m i (x - y)) = f (update m i x) - f (update m i y) :=
  f.toMultilinearMap.map_update_sub _ _ _ _

@[deprecated (since := "2024-11-03")]
protected alias map_sub := ContinuousMultilinearMap.map_update_sub

section IsTopologicalAddGroup

variable [IsTopologicalAddGroup M‚ÇÇ]

instance : Neg (ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ) :=
  ‚ü®fun f => { -f.toMultilinearMap with cont := f.cont.neg }‚ü©

@[simp]
theorem neg_apply (m : ‚àÄ i, M‚ÇÅ i) : (-f) m = -f m :=
  rfl

instance : Sub (ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ) :=
  ‚ü®fun f g => { f.toMultilinearMap - g.toMultilinearMap with cont := f.cont.sub g.cont }‚ü©

@[simp]
theorem sub_apply (m : ‚àÄ i, M‚ÇÅ i) : (f - f') m = f m - f' m :=
  rfl

instance : AddCommGroup (ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ) :=
  toMultilinearMap_injective.addCommGroup _ rfl (fun _ _ => rfl) (fun _ => rfl) (fun _ _ => rfl)
    (fun _ _ => rfl) fun _ _ => rfl

theorem neg_prod_neg [AddCommGroup M‚ÇÉ] [Module R M‚ÇÉ] [TopologicalSpace M‚ÇÉ]
    [IsTopologicalAddGroup M‚ÇÉ] (f : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ)
    (g : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÉ) : (-f).prod (-g) = - f.prod g :=
  rfl

theorem sub_prod_sub [AddCommGroup M‚ÇÉ] [Module R M‚ÇÉ] [TopologicalSpace M‚ÇÉ]
    [IsTopologicalAddGroup M‚ÇÉ] (f‚ÇÅ f‚ÇÇ : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ)
    (g‚ÇÅ g‚ÇÇ : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÉ) :
    (f‚ÇÅ - f‚ÇÇ).prod (g‚ÇÅ - g‚ÇÇ) = f‚ÇÅ.prod g‚ÇÅ - f‚ÇÇ.prod g‚ÇÇ :=
  rfl

end IsTopologicalAddGroup

end Ring

section CommSemiring

variable [CommSemiring R] [‚àÄ i, AddCommMonoid (M‚ÇÅ i)] [AddCommMonoid M‚ÇÇ] [‚àÄ i, Module R (M‚ÇÅ i)]
  [Module R M‚ÇÇ] [‚àÄ i, TopologicalSpace (M‚ÇÅ i)] [TopologicalSpace M‚ÇÇ]
  (f : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ)

theorem map_piecewise_smul [DecidableEq Œπ] (c : Œπ ‚Üí R) (m : ‚àÄ i, M‚ÇÅ i) (s : Finset Œπ) :
    f (s.piecewise (fun i => c i ‚Ä¢ m i) m) = (‚àè i ‚àà s, c i) ‚Ä¢ f m :=
  f.toMultilinearMap.map_piecewise_smul _ _ _

/-- Multiplicativity of a continuous multilinear map along all coordinates at the same time,
writing `f (fun i ‚Ü¶ c i ‚Ä¢ m i)` as `(‚àè i, c i) ‚Ä¢ f m`. -/
theorem map_smul_univ [Fintype Œπ] (c : Œπ ‚Üí R) (m : ‚àÄ i, M‚ÇÅ i) :
    (f fun i => c i ‚Ä¢ m i) = (‚àè i, c i) ‚Ä¢ f m :=
  f.toMultilinearMap.map_smul_univ _ _

/-- If two continuous `R`-multilinear maps from `R` are equal on 1, then they are equal.

This is the multilinear version of `ContinuousLinearMap.ext_ring`. -/
@[ext]
theorem ext_ring [Finite Œπ] [TopologicalSpace R]
    ‚¶Éf g : ContinuousMultilinearMap R (fun _ : Œπ => R) M‚ÇÇ‚¶Ñ
    (h : f (fun _ ‚Ü¶ 1) = g (fun _ ‚Ü¶ 1)) : f = g :=
  toMultilinearMap_injective <| MultilinearMap.ext_ring h

end CommSemiring

section DistribMulAction

variable {R' R'' A : Type*} [Monoid R'] [Monoid R''] [Semiring A] [‚àÄ i, AddCommMonoid (M‚ÇÅ i)]
  [AddCommMonoid M‚ÇÇ] [‚àÄ i, TopologicalSpace (M‚ÇÅ i)] [TopologicalSpace M‚ÇÇ] [‚àÄ i, Module A (M‚ÇÅ i)]
  [Module A M‚ÇÇ] [DistribMulAction R' M‚ÇÇ] [ContinuousConstSMul R' M‚ÇÇ] [SMulCommClass A R' M‚ÇÇ]
  [DistribMulAction R'' M‚ÇÇ] [ContinuousConstSMul R'' M‚ÇÇ] [SMulCommClass A R'' M‚ÇÇ]

instance [ContinuousAdd M‚ÇÇ] : DistribMulAction R' (ContinuousMultilinearMap A M‚ÇÅ M‚ÇÇ) :=
  Function.Injective.distribMulAction
    { toFun := toMultilinearMap,
      map_zero' := toMultilinearMap_zero,
      map_add' := toMultilinearMap_add }
    toMultilinearMap_injective
    fun _ _ => rfl

end DistribMulAction

section Module

variable {R' A : Type*} [Semiring R'] [Semiring A] [‚àÄ i, AddCommMonoid (M‚ÇÅ i)] [AddCommMonoid M‚ÇÇ]
  [‚àÄ i, TopologicalSpace (M‚ÇÅ i)] [TopologicalSpace M‚ÇÇ] [ContinuousAdd M‚ÇÇ] [‚àÄ i, Module A (M‚ÇÅ i)]
  [Module A M‚ÇÇ] [Module R' M‚ÇÇ] [ContinuousConstSMul R' M‚ÇÇ] [SMulCommClass A R' M‚ÇÇ]

/-- The space of continuous multilinear maps over an algebra over `R` is a module over `R`, for the
pointwise addition and scalar multiplication. -/
instance : Module R' (ContinuousMultilinearMap A M‚ÇÅ M‚ÇÇ) :=
  Function.Injective.module _
    { toFun := toMultilinearMap,
      map_zero' := toMultilinearMap_zero,
      map_add' := toMultilinearMap_add }
    toMultilinearMap_injective fun _ _ => rfl

/-- Linear map version of the map `toMultilinearMap` associating to a continuous multilinear map
the corresponding multilinear map. -/
@[simps]
def toMultilinearMapLinear : ContinuousMultilinearMap A M‚ÇÅ M‚ÇÇ ‚Üí‚Çó[R'] MultilinearMap A M‚ÇÅ M‚ÇÇ where
  toFun := toMultilinearMap
  map_add' := toMultilinearMap_add
  map_smul' := toMultilinearMap_smul

/-- `ContinuousMultilinearMap.pi` as a `LinearEquiv`. -/
@[simps +simpRhs]
def piLinearEquiv {Œπ' : Type*} {M' : Œπ' ‚Üí Type*} [‚àÄ i, AddCommMonoid (M' i)]
    [‚àÄ i, TopologicalSpace (M' i)] [‚àÄ i, ContinuousAdd (M' i)] [‚àÄ i, Module R' (M' i)]
    [‚àÄ i, Module A (M' i)] [‚àÄ i, SMulCommClass A R' (M' i)] [‚àÄ i, ContinuousConstSMul R' (M' i)] :
    (‚àÄ i, ContinuousMultilinearMap A M‚ÇÅ (M' i)) ‚âÉ‚Çó[R'] ContinuousMultilinearMap A M‚ÇÅ (‚àÄ i, M' i) :=
  { piEquiv with
    map_add' := fun _ _ => rfl
    map_smul' := fun _ _ => rfl }

end Module

section CommAlgebra

variable (R Œπ) (A : Type*) [Fintype Œπ] [CommSemiring R] [CommSemiring A] [Algebra R A]
  [TopologicalSpace A] [ContinuousMul A]

/-- The continuous multilinear map on `A^Œπ`, where `A` is a normed commutative algebra
over `ùïú`, associating to `m` the product of all the `m i`.

See also `ContinuousMultilinearMap.mkPiAlgebraFin`. -/
protected def mkPiAlgebra : ContinuousMultilinearMap R (fun _ : Œπ => A) A where
  cont := continuous_finset_prod _ fun _ _ => continuous_apply _
  toMultilinearMap := MultilinearMap.mkPiAlgebra R Œπ A

@[simp]
theorem mkPiAlgebra_apply (m : Œπ ‚Üí A) : ContinuousMultilinearMap.mkPiAlgebra R Œπ A m = ‚àè i, m i :=
  rfl

end CommAlgebra

section Algebra

variable (R n) (A : Type*) [CommSemiring R] [Semiring A] [Algebra R A] [TopologicalSpace A]
  [ContinuousMul A]

/-- The continuous multilinear map on `A^n`, where `A` is a normed algebra over `ùïú`, associating to
`m` the product of all the `m i`.

See also: `ContinuousMultilinearMap.mkPiAlgebra`. -/
protected def mkPiAlgebraFin : A[√ón]‚ÜíL[R] A where
  cont := by
    change Continuous fun m => (List.ofFn m).prod
    simp_rw [List.ofFn_eq_map]
    exact continuous_list_prod _ fun i _ => continuous_apply _
  toMultilinearMap := MultilinearMap.mkPiAlgebraFin R n A

variable {R n A}

@[simp]
theorem mkPiAlgebraFin_apply (m : Fin n ‚Üí A) :
    ContinuousMultilinearMap.mkPiAlgebraFin R n A m = (List.ofFn m).prod :=
  rfl

end Algebra

section SMulRight

variable [CommSemiring R] [‚àÄ i, AddCommMonoid (M‚ÇÅ i)] [AddCommMonoid M‚ÇÇ] [‚àÄ i, Module R (M‚ÇÅ i)]
  [Module R M‚ÇÇ] [TopologicalSpace R] [‚àÄ i, TopologicalSpace (M‚ÇÅ i)] [TopologicalSpace M‚ÇÇ]
  [ContinuousSMul R M‚ÇÇ] (f : ContinuousMultilinearMap R M‚ÇÅ R) (z : M‚ÇÇ)

/-- Given a continuous `R`-multilinear map `f` taking values in `R`, `f.smulRight z` is the
continuous multilinear map sending `m` to `f m ‚Ä¢ z`. -/
@[simps! toMultilinearMap apply]
def smulRight : ContinuousMultilinearMap R M‚ÇÅ M‚ÇÇ where
  toMultilinearMap := f.toMultilinearMap.smulRight z
  cont := f.cont.smul continuous_const

end SMulRight

section CommRing
variable {M : Type*}
variable [Fintype Œπ] [CommRing R] [AddCommMonoid M] [Module R M]
variable [TopologicalSpace R] [TopologicalSpace M]
variable [ContinuousMul R] [ContinuousSMul R M]

variable (R Œπ) in
/-- The canonical continuous multilinear map on `R^Œπ`, associating to `m` the product of all the
`m i` (multiplied by a fixed reference element `z` in the target module) -/
protected def mkPiRing (z : M) : ContinuousMultilinearMap R (fun _ : Œπ => R) M :=
  (ContinuousMultilinearMap.mkPiAlgebra R Œπ R).smulRight z


@[simp]
theorem mkPiRing_apply (z : M) (m : Œπ ‚Üí R) :
    (ContinuousMultilinearMap.mkPiRing R Œπ z : (Œπ ‚Üí R) ‚Üí M) m = (‚àè i, m i) ‚Ä¢ z :=
  rfl

theorem mkPiRing_apply_one_eq_self (f : ContinuousMultilinearMap R (fun _ : Œπ => R) M) :
    ContinuousMultilinearMap.mkPiRing R Œπ (f fun _ => 1) = f :=
  toMultilinearMap_injective f.toMultilinearMap.mkPiRing_apply_one_eq_self

theorem mkPiRing_eq_iff {z‚ÇÅ z‚ÇÇ : M} :
    ContinuousMultilinearMap.mkPiRing R Œπ z‚ÇÅ = ContinuousMultilinearMap.mkPiRing R Œπ z‚ÇÇ ‚Üî
      z‚ÇÅ = z‚ÇÇ := by
  rw [‚Üê toMultilinearMap_injective.eq_iff]
  exact MultilinearMap.mkPiRing_eq_iff

theorem mkPiRing_zero : ContinuousMultilinearMap.mkPiRing R Œπ (0 : M) = 0 := by
  ext; rw [mkPiRing_apply, smul_zero, ContinuousMultilinearMap.zero_apply]

theorem mkPiRing_eq_zero_iff (z : M) : ContinuousMultilinearMap.mkPiRing R Œπ z = 0 ‚Üî z = 0 := by
  rw [‚Üê mkPiRing_zero, mkPiRing_eq_iff]

end CommRing

end ContinuousMultilinearMap
