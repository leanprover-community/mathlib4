/-
Copyright (c) 2023 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
module

public import Mathlib.Topology.Algebra.Module.Equiv
public import Mathlib.Topology.Algebra.Module.Multilinear.Bounded
public import Mathlib.Topology.Algebra.Module.StrongTopology
public import Mathlib.Topology.Algebra.Module.UniformConvergence
public import Mathlib.Topology.Algebra.SeparationQuotient.Section
public import Mathlib.Topology.Hom.ContinuousEvalConst
public import Mathlib.Topology.Algebra.InfiniteSum.Basic

/-!
# Topology on continuous multilinear maps

In this file we define `TopologicalSpace` and `UniformSpace` structures
on `ContinuousMultilinearMap ğ•œ E F`,
where `E i` is a family of vector spaces over `ğ•œ` with topologies
and `F` is a topological vector space.
-/

@[expose] public section

open Bornology Function Set Topology
open scoped UniformConvergence Filter

namespace ContinuousMultilinearMap

variable {ğ•œ Î¹ : Type*} {E : Î¹ â†’ Type*} {F : Type*}
  [NormedField ğ•œ]
  [âˆ€ i, TopologicalSpace (E i)] [âˆ€ i, AddCommGroup (E i)] [âˆ€ i, Module ğ•œ (E i)]
  [AddCommGroup F] [Module ğ•œ F]

/-- An auxiliary definition used to define topology on `ContinuousMultilinearMap ğ•œ E F`. -/
def toUniformOnFun [TopologicalSpace F] (f : ContinuousMultilinearMap ğ•œ E F) :
    (Î  i, E i) â†’áµ¤[{s | IsVonNBounded ğ•œ s}] F :=
  UniformOnFun.ofFun _ f

open UniformOnFun in
lemma range_toUniformOnFun [DecidableEq Î¹] [TopologicalSpace F] :
    range toUniformOnFun =
      {f : (Î  i, E i) â†’áµ¤[{s | IsVonNBounded ğ•œ s}] F |
        Continuous (toFun _ f) âˆ§
        (âˆ€ (m : Î  i, E i) i x y,
          toFun _ f (update m i (x + y)) = toFun _ f (update m i x) + toFun _ f (update m i y)) âˆ§
        (âˆ€ (m : Î  i, E i) i (c : ğ•œ) x,
          toFun _ f (update m i (c â€¢ x)) = c â€¢ toFun _ f (update m i x))} := by
  ext f
  constructor
  Â· rintro âŸ¨f, rflâŸ©
    exact âŸ¨f.cont, f.map_update_add, f.map_update_smulâŸ©
  Â· rintro âŸ¨hcont, hadd, hsmulâŸ©
    exact âŸ¨âŸ¨âŸ¨f, by intro; convert hadd, by intro; convert hsmulâŸ©, hcontâŸ©, rflâŸ©

@[simp]
lemma toUniformOnFun_toFun [TopologicalSpace F] (f : ContinuousMultilinearMap ğ•œ E F) :
    UniformOnFun.toFun _ f.toUniformOnFun = f :=
  rfl

instance instTopologicalSpace [TopologicalSpace F] [IsTopologicalAddGroup F] :
    TopologicalSpace (ContinuousMultilinearMap ğ•œ E F) :=
  .induced toUniformOnFun <|
    @UniformOnFun.topologicalSpace _ _ (IsTopologicalAddGroup.rightUniformSpace F) _

instance instUniformSpace [UniformSpace F] [IsUniformAddGroup F] :
    UniformSpace (ContinuousMultilinearMap ğ•œ E F) :=
  .replaceTopology (.comap toUniformOnFun <| UniformOnFun.uniformSpace _ _ _) <| by
    rw [instTopologicalSpace, IsUniformAddGroup.rightUniformSpace_eq]; rfl

section IsUniformAddGroup

variable [UniformSpace F] [IsUniformAddGroup F]

lemma isUniformInducing_toUniformOnFun :
    IsUniformInducing (toUniformOnFun :
      ContinuousMultilinearMap ğ•œ E F â†’ ((Î  i, E i) â†’áµ¤[{s | IsVonNBounded ğ•œ s}] F)) := âŸ¨rflâŸ©

lemma isUniformEmbedding_toUniformOnFun :
    IsUniformEmbedding (toUniformOnFun : ContinuousMultilinearMap ğ•œ E F â†’ _) :=
  âŸ¨isUniformInducing_toUniformOnFun, DFunLike.coe_injectiveâŸ©

lemma isEmbedding_toUniformOnFun :
    IsEmbedding (toUniformOnFun : ContinuousMultilinearMap ğ•œ E F â†’
      ((Î  i, E i) â†’áµ¤[{s | IsVonNBounded ğ•œ s}] F)) :=
  isUniformEmbedding_toUniformOnFun.isEmbedding

theorem uniformContinuous_coe_fun [âˆ€ i, ContinuousSMul ğ•œ (E i)] :
    UniformContinuous (DFunLike.coe : ContinuousMultilinearMap ğ•œ E F â†’ (Î  i, E i) â†’ F) :=
  (UniformOnFun.uniformContinuous_toFun sUnion_isVonNBounded_eq_univ).comp
    isUniformEmbedding_toUniformOnFun.uniformContinuous

theorem uniformContinuous_eval_const [âˆ€ i, ContinuousSMul ğ•œ (E i)] (x : Î  i, E i) :
    UniformContinuous fun f : ContinuousMultilinearMap ğ•œ E F â†¦ f x :=
  uniformContinuous_pi.1 uniformContinuous_coe_fun x

instance instIsUniformAddGroup : IsUniformAddGroup (ContinuousMultilinearMap ğ•œ E F) :=
  let Ï† : ContinuousMultilinearMap ğ•œ E F â†’+ (Î  i, E i) â†’áµ¤[{s | IsVonNBounded ğ•œ s}] F :=
    { toFun := toUniformOnFun, map_add' := fun _ _ â†¦ rfl, map_zero' := rfl }
  isUniformEmbedding_toUniformOnFun.isUniformAddGroup Ï†

instance instUniformContinuousConstSMul {M : Type*}
    [Monoid M] [DistribMulAction M F] [SMulCommClass ğ•œ M F] [ContinuousConstSMul M F] :
    UniformContinuousConstSMul M (ContinuousMultilinearMap ğ•œ E F) :=
  haveI := uniformContinuousConstSMul_of_continuousConstSMul M F
  isUniformEmbedding_toUniformOnFun.uniformContinuousConstSMul fun _ _ â†¦ rfl

theorem isUniformInducing_postcomp
    {G : Type*} [AddCommGroup G] [UniformSpace G] [IsUniformAddGroup G] [Module ğ•œ G]
    (g : F â†’L[ğ•œ] G) (hg : IsUniformInducing g) :
    IsUniformInducing (g.compContinuousMultilinearMap :
      ContinuousMultilinearMap ğ•œ E F â†’ ContinuousMultilinearMap ğ•œ E G) := by
  rw [â† isUniformInducing_toUniformOnFun.of_comp_iff]
  exact (UniformOnFun.postcomp_isUniformInducing hg).comp isUniformInducing_toUniformOnFun

section CompleteSpace

variable [âˆ€ i, ContinuousSMul ğ•œ (E i)] [ContinuousConstSMul ğ•œ F] [CompleteSpace F]

open UniformOnFun in
theorem completeSpace (h : IsCoherentWith {s : Set (Î  i, E i) | IsVonNBounded ğ•œ s}) :
    CompleteSpace (ContinuousMultilinearMap ğ•œ E F) := by
  classical
  wlog hF : T2Space F generalizing F
  Â· rw [(isUniformInducing_postcomp (SeparationQuotient.mkCLM _ _)
      SeparationQuotient.isUniformInducing_mk).completeSpace_congr]
    Â· exact this inferInstance
    Â· intro f
      use (SeparationQuotient.outCLM _ _).compContinuousMultilinearMap f
      simp [DFunLike.ext_iff]
  have H : âˆ€ {m : Î  i, E i},
      Continuous fun f : (Î  i, E i) â†’áµ¤[{s | IsVonNBounded ğ•œ s}] F â†¦ toFun _ f m :=
    (uniformContinuous_eval (sUnion_isVonNBounded_eq_univ) _).continuous
  rw [completeSpace_iff_isComplete_range isUniformInducing_toUniformOnFun, range_toUniformOnFun]
  simp only [setOf_and, setOf_forall]
  apply_rules [IsClosed.isComplete, IsClosed.inter]
  Â· exact UniformOnFun.isClosed_setOf_continuous h
  Â· exact isClosed_iInter fun m â†¦ isClosed_iInter fun i â†¦
      isClosed_iInter fun x â†¦ isClosed_iInter fun y â†¦ isClosed_eq H (H.add H)
  Â· exact isClosed_iInter fun m â†¦ isClosed_iInter fun i â†¦
      isClosed_iInter fun c â†¦ isClosed_iInter fun x â†¦ isClosed_eq H (H.const_smul _)

instance instCompleteSpace [âˆ€ i, IsTopologicalAddGroup (E i)] [SequentialSpace (Î  i, E i)] :
    CompleteSpace (ContinuousMultilinearMap ğ•œ E F) :=
  completeSpace <| .of_seq fun _u x hux â†¦ (hux.isVonNBounded_range ğ•œ).insert x

end CompleteSpace

section RestrictScalars

variable (ğ•œ' : Type*) [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ' ğ•œ]
  [âˆ€ i, Module ğ•œ' (E i)] [âˆ€ i, IsScalarTower ğ•œ' ğ•œ (E i)] [Module ğ•œ' F] [IsScalarTower ğ•œ' ğ•œ F]
  [âˆ€ i, ContinuousSMul ğ•œ (E i)]

theorem isUniformEmbedding_restrictScalars :
    IsUniformEmbedding
      (restrictScalars ğ•œ' : ContinuousMultilinearMap ğ•œ E F â†’ ContinuousMultilinearMap ğ•œ' E F) := by
  letI : NontriviallyNormedField ğ•œ :=
    âŸ¨let âŸ¨x, hxâŸ© := @NontriviallyNormedField.non_trivial ğ•œ' _; âŸ¨algebraMap ğ•œ' ğ•œ x, by simpaâŸ©âŸ©
  rw [â† isUniformEmbedding_toUniformOnFun.of_comp_iff]
  convert isUniformEmbedding_toUniformOnFun using 4 with s
  exact âŸ¨fun h â†¦ h.extend_scalars _, fun h â†¦ h.restrict_scalars _âŸ©

theorem uniformContinuous_restrictScalars :
    UniformContinuous
      (restrictScalars ğ•œ' : ContinuousMultilinearMap ğ•œ E F â†’ ContinuousMultilinearMap ğ•œ' E F) :=
  (isUniformEmbedding_restrictScalars ğ•œ').uniformContinuous

end RestrictScalars

end IsUniformAddGroup

variable [TopologicalSpace F] [IsTopologicalAddGroup F]

instance instIsTopologicalAddGroup : IsTopologicalAddGroup (ContinuousMultilinearMap ğ•œ E F) :=
  letI := IsTopologicalAddGroup.rightUniformSpace F
  haveI := isUniformAddGroup_of_addCommGroup (G := F)
  inferInstance

instance instContinuousConstSMul
    {M : Type*} [Monoid M] [DistribMulAction M F] [SMulCommClass ğ•œ M F] [ContinuousConstSMul M F] :
    ContinuousConstSMul M (ContinuousMultilinearMap ğ•œ E F) := by
  letI := IsTopologicalAddGroup.rightUniformSpace F
  haveI := isUniformAddGroup_of_addCommGroup (G := F)
  infer_instance

instance instContinuousSMul [ContinuousSMul ğ•œ F] :
    ContinuousSMul ğ•œ (ContinuousMultilinearMap ğ•œ E F) :=
  letI := IsTopologicalAddGroup.rightUniformSpace F
  haveI := isUniformAddGroup_of_addCommGroup (G := F)
  let Ï† : ContinuousMultilinearMap ğ•œ E F â†’â‚—[ğ•œ] (Î  i, E i) â†’ F :=
    { toFun := (â†‘), map_add' := fun _ _ â†¦ rfl, map_smul' := fun _ _ â†¦ rfl }
  UniformOnFun.continuousSMul_induced_of_image_bounded _ _ _ _ Ï†
    isEmbedding_toUniformOnFun.isInducing fun _ _ hu â†¦ hu.image_multilinear _

theorem hasBasis_nhds_zero_of_basis {Î¹ : Type*} {p : Î¹ â†’ Prop} {b : Î¹ â†’ Set F}
    (h : (ğ“ (0 : F)).HasBasis p b) :
    (ğ“ (0 : ContinuousMultilinearMap ğ•œ E F)).HasBasis
      (fun Si : Set (Î  i, E i) Ã— Î¹ => IsVonNBounded ğ•œ Si.1 âˆ§ p Si.2)
      fun Si => { f | MapsTo f Si.1 (b Si.2) } := by
  letI : UniformSpace F := IsTopologicalAddGroup.rightUniformSpace F
  haveI : IsUniformAddGroup F := isUniformAddGroup_of_addCommGroup
  rw [nhds_induced]
  refine (UniformOnFun.hasBasis_nhds_zero_of_basis _ ?_ ?_ h).comap DFunLike.coe
  Â· exact âŸ¨âˆ…, isVonNBounded_empty _ _âŸ©
  Â· exact directedOn_of_sup_mem fun _ _ => Bornology.IsVonNBounded.union

theorem hasBasis_nhds_zero :
    (ğ“ (0 : ContinuousMultilinearMap ğ•œ E F)).HasBasis
      (fun SV : Set (Î  i, E i) Ã— Set F => IsVonNBounded ğ•œ SV.1 âˆ§ SV.2 âˆˆ ğ“ 0) fun SV =>
      { f | MapsTo f SV.1 SV.2 } :=
  hasBasis_nhds_zero_of_basis (Filter.basis_sets _)

theorem eventually_nhds_zero_mapsTo {s : Set (âˆ€ i, E i)} (hs : IsVonNBounded ğ•œ s)
    {U : Set F} (hu : U âˆˆ ğ“ 0) :
    âˆ€á¶  f : ContinuousMultilinearMap ğ•œ E F in ğ“ 0, MapsTo f s U :=
  hasBasis_nhds_zero.mem_of_mem (i := (s, U)) âŸ¨hs, huâŸ©

theorem isVonNBounded_image2_apply [ContinuousConstSMul ğ•œ F]
    {S : Set (ContinuousMultilinearMap ğ•œ E F)} (hS : IsVonNBounded ğ•œ S)
    {s : Set (âˆ€ i, E i)} (hs : IsVonNBounded ğ•œ s) :
    IsVonNBounded ğ•œ (Set.image2 (fun f x â†¦ f x) S s) := by
  intro U hU
  filter_upwards [hS (eventually_nhds_zero_mapsTo hs hU)] with c hc
  rw [image2_subset_iff]
  intro f hf x hx
  rcases hc hf with âŸ¨g, hg, rflâŸ©
  exact smul_mem_smul_set (hg hx)

section CompContinuousLinearMap
variable {Eâ‚ : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (Eâ‚ i)] [ContinuousConstSMul ğ•œ F]
  [âˆ€ i, AddCommGroup (Eâ‚ i)] [âˆ€ i, Module ğ•œ (Eâ‚ i)]

/-- `ContinuousMultilinearMap.compContinuousLinearMap` as a bundled continuous linear map.
This implementation fixes `f : Î  i, E i â†’L[ğ•œ] Eâ‚ i`.

Actually, the map is multilinear in `f`,
see `ContinuousMultilinearMap.compContinuousLinearMapContinuousMultilinear`.

For a version fixing `g` and varying `f`, see `compContinuousLinearMapLRight`. -/
@[simps! apply]
def compContinuousLinearMapL (f : âˆ€ i, E i â†’L[ğ•œ] Eâ‚ i) :
    ContinuousMultilinearMap ğ•œ Eâ‚ F â†’L[ğ•œ] ContinuousMultilinearMap ğ•œ E F :=
  let aux : ContinuousMultilinearMap ğ•œ Eâ‚ F â†’â‚—[ğ•œ] ContinuousMultilinearMap ğ•œ E F :=
    { toFun g := g.compContinuousLinearMap f
      map_add' _ _ := by ext; simp
      map_smul' _ _ := by ext; simp }
  { toLinearMap := aux
    cont := by
      apply continuous_of_tendsto_nhds_zero (f := aux)
      rw [hasBasis_nhds_zero.tendsto_iff hasBasis_nhds_zero]
      rintro âŸ¨U, VâŸ© âŸ¨hU, hVâŸ©
      set Ï† : (âˆ€ i, E i) â†’L[ğ•œ] (âˆ€ i, Eâ‚ i) :=
        .pi fun i â†¦ f i âˆ˜L .proj i
      exact âŸ¨(Ï† '' U, V), âŸ¨hU.image Ï†, hVâŸ©, fun g hg â†¦ hg.comp (mapsTo_image _ _)âŸ© }

end CompContinuousLinearMap

variable [âˆ€ i, ContinuousSMul ğ•œ (E i)]

instance : ContinuousEvalConst (ContinuousMultilinearMap ğ•œ E F) (Î  i, E i) F where
  continuous_eval_const x :=
    let _ := IsTopologicalAddGroup.rightUniformSpace F
    have _ := isUniformAddGroup_of_addCommGroup (G := F)
    (uniformContinuous_eval_const x).continuous

instance instT2Space [T2Space F] : T2Space (ContinuousMultilinearMap ğ•œ E F) :=
  .of_injective_continuous DFunLike.coe_injective continuous_coeFun

instance instT3Space [T2Space F] : T3Space (ContinuousMultilinearMap ğ•œ E F) :=
  inferInstance

section RestrictScalars

variable {ğ•œ' : Type*} [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ' ğ•œ]
  [âˆ€ i, Module ğ•œ' (E i)] [âˆ€ i, IsScalarTower ğ•œ' ğ•œ (E i)] [Module ğ•œ' F] [IsScalarTower ğ•œ' ğ•œ F]

theorem isEmbedding_restrictScalars :
    IsEmbedding
      (restrictScalars ğ•œ' : ContinuousMultilinearMap ğ•œ E F â†’ ContinuousMultilinearMap ğ•œ' E F) :=
  letI : UniformSpace F := IsTopologicalAddGroup.rightUniformSpace F
  haveI : IsUniformAddGroup F := isUniformAddGroup_of_addCommGroup
  (isUniformEmbedding_restrictScalars _).isEmbedding

@[continuity, fun_prop]
theorem continuous_restrictScalars :
    Continuous
      (restrictScalars ğ•œ' : ContinuousMultilinearMap ğ•œ E F â†’ ContinuousMultilinearMap ğ•œ' E F) :=
  isEmbedding_restrictScalars.continuous

variable (ğ•œ') in
/-- `ContinuousMultilinearMap.restrictScalars` as a `ContinuousLinearMap`. -/
@[simps -fullyApplied apply]
def restrictScalarsLinear [ContinuousConstSMul ğ•œ' F] :
    ContinuousMultilinearMap ğ•œ E F â†’L[ğ•œ'] ContinuousMultilinearMap ğ•œ' E F where
  toFun := restrictScalars ğ•œ'
  map_add' _ _ := rfl
  map_smul' _ _ := rfl

end RestrictScalars

variable (ğ•œ E F)

/-- The application of a multilinear map as a `ContinuousLinearMap`. -/
def apply [ContinuousConstSMul ğ•œ F] (m : Î  i, E i) : ContinuousMultilinearMap ğ•œ E F â†’L[ğ•œ] F where
  toFun c := c m
  map_add' _ _ := rfl
  map_smul' _ _ := rfl
  cont := continuous_eval_const m

variable {ğ•œ E F}

@[simp]
lemma apply_apply [ContinuousConstSMul ğ•œ F] {m : Î  i, E i} {c : ContinuousMultilinearMap ğ•œ E F} :
    apply ğ•œ E F m c = c m := rfl

theorem hasSum_eval {Î± : Type*} {p : Î± â†’ ContinuousMultilinearMap ğ•œ E F}
    {q : ContinuousMultilinearMap ğ•œ E F} (h : HasSum p q) (m : Î  i, E i) :
    HasSum (fun a => p a m) (q m) :=
  h.map (applyAddHom m) (continuous_eval_const m)

theorem tsum_eval [T2Space F] {Î± : Type*} {p : Î± â†’ ContinuousMultilinearMap ğ•œ E F} (hp : Summable p)
    (m : Î  i, E i) : (âˆ‘' a, p a) m = âˆ‘' a, p a m :=
  (hasSum_eval hp.hasSum m).tsum_eq.symm

end ContinuousMultilinearMap

namespace ContinuousLinearMap

variable {ğ•œ Î¹ : Type*} {E : Î¹ â†’ Type*} {F G : Type*} [NormedField ğ•œ] [âˆ€ i, TopologicalSpace (E i)]
  [âˆ€ i, AddCommGroup (E i)] [âˆ€ i, Module ğ•œ (E i)]
  [AddCommGroup F] [Module ğ•œ F] [TopologicalSpace F] [IsTopologicalAddGroup F]
  [ContinuousConstSMul ğ•œ F]
  [AddCommGroup G] [Module ğ•œ G] [TopologicalSpace G] [IsTopologicalAddGroup G]
  [ContinuousConstSMul ğ•œ G]

variable (ğ•œ E F G) in
/-- `ContinuousLinearMap.compContinuousMultilinearMap` as a bundled continuous bilinear map. -/
def compContinuousMultilinearMapL :
    (F â†’L[ğ•œ] G) â†’L[ğ•œ] ContinuousMultilinearMap ğ•œ E F â†’L[ğ•œ] ContinuousMultilinearMap ğ•œ E G :=
  let aux : (F â†’L[ğ•œ] G) â†’â‚—[ğ•œ] ContinuousMultilinearMap ğ•œ E F â†’L[ğ•œ] ContinuousMultilinearMap ğ•œ E G :=
    { toFun g :=
        let auxâ‚ : ContinuousMultilinearMap ğ•œ E F â†’â‚—[ğ•œ] ContinuousMultilinearMap ğ•œ E G :=
          { toFun := g.compContinuousMultilinearMap
            map_add' _ _ := by ext; simp
            map_smul' _ _ := by ext; simp }
        { toLinearMap := auxâ‚
          cont := by
            apply continuous_of_tendsto_nhds_zero (f := auxâ‚)
            rw [ContinuousMultilinearMap.hasBasis_nhds_zero.tendsto_iff
              ContinuousMultilinearMap.hasBasis_nhds_zero]
            rintro âŸ¨U, VâŸ© âŸ¨hU, hVâŸ©
            refine âŸ¨(U, g â»Â¹' V), âŸ¨hU, ?_âŸ©, ?_âŸ©
            Â· exact (map_continuous g).tendsto 0 <| by simpa
            Â· exact fun f hf â†¦ hf
        }
      map_add' _ _ := by ext; simp
      map_smul' _ _ := by ext; simp }
  { toLinearMap := aux
    cont := by
      apply continuous_of_tendsto_nhds_zero (f := aux)
      rw [ContinuousLinearMap.hasBasis_nhds_zero.tendsto_iff <|
        ContinuousLinearMap.hasBasis_nhds_zero_of_basis <|
        ContinuousMultilinearMap.hasBasis_nhds_zero]
      rintro âŸ¨U, V, WâŸ© âŸ¨hU, hV, hWâŸ©
      refine âŸ¨(.image2 (fun f v â†¦ f v) U V, W), âŸ¨?_, hWâŸ©, ?_âŸ©
      Â· exact ContinuousMultilinearMap.isVonNBounded_image2_apply hU hV
      Â· exact fun g hg f hf m hm â†¦ hg _ <| mem_image2_of_mem hf hm }

@[simp]
theorem compContinuousMultilinearMapL_apply (g : F â†’L[ğ•œ] G) (f : ContinuousMultilinearMap ğ•œ E F) :
    compContinuousMultilinearMapL ğ•œ E F G g f = g.compContinuousMultilinearMap f :=
  rfl

end ContinuousLinearMap

namespace ContinuousLinearEquiv

variable {ğ•œ Î¹ : Type*} {E Eâ‚ : Î¹ â†’ Type*} {F G : Type*} [NormedField ğ•œ]
  [âˆ€ i, TopologicalSpace (E i)] [âˆ€ i, AddCommGroup (E i)] [âˆ€ i, Module ğ•œ (E i)]
  [âˆ€ i, TopologicalSpace (Eâ‚ i)] [âˆ€ i, AddCommGroup (Eâ‚ i)] [âˆ€ i, Module ğ•œ (Eâ‚ i)]
  [AddCommGroup F] [Module ğ•œ F] [TopologicalSpace F] [IsTopologicalAddGroup F]
  [ContinuousConstSMul ğ•œ F]
  [AddCommGroup G] [Module ğ•œ G] [TopologicalSpace G] [IsTopologicalAddGroup G]
  [ContinuousConstSMul ğ•œ G]

variable (F) in
/-- `ContinuousMultilinearMap.compContinuousLinearMap` as a bundled continuous linear equiv,
given `f : Î  i, E i â‰ƒL[ğ•œ] Eâ‚ i`. -/
def continuousMultilinearMapCongrLeft (f : âˆ€ i, E i â‰ƒL[ğ•œ] Eâ‚ i) :
    ContinuousMultilinearMap ğ•œ Eâ‚ F â‰ƒL[ğ•œ] ContinuousMultilinearMap ğ•œ E F where
  __ := ContinuousMultilinearMap.compContinuousLinearMapL fun i â†¦ f i
  invFun := ContinuousMultilinearMap.compContinuousLinearMapL fun i â†¦ (f i).symm
  left_inv g := by ext; simp
  right_inv g := by ext; simp

@[simp]
theorem continuousMultilinearMapCongrLeft_symm
    (f : âˆ€ i, E i â‰ƒL[ğ•œ] Eâ‚ i) :
    (ContinuousLinearEquiv.continuousMultilinearMapCongrLeft F f).symm =
      .continuousMultilinearMapCongrLeft F fun i : Î¹ => (f i).symm :=
  rfl

@[simp]
theorem continuousMultilinearMapCongrLeft_apply
    (g : ContinuousMultilinearMap ğ•œ Eâ‚ F) (f : âˆ€ i, E i â‰ƒL[ğ•œ] Eâ‚ i) :
    ContinuousLinearEquiv.continuousMultilinearMapCongrLeft F f g =
      g.compContinuousLinearMap fun i => (f i : E i â†’L[ğ•œ] Eâ‚ i) :=
  rfl

variable (E) in
/-- `ContinuousLinearMap.compContinuousMultilinearMap` as a bundled
continuous linear equiv. -/
def continuousMultilinearMapCongrRight (g : F â‰ƒL[ğ•œ] G) :
    ContinuousMultilinearMap ğ•œ E F â‰ƒL[ğ•œ] ContinuousMultilinearMap ğ•œ E G where
  __ := ContinuousLinearMap.compContinuousMultilinearMapL _ _ _ _ g
  invFun := ContinuousLinearMap.compContinuousMultilinearMapL _ _ _ _ g.symm
  left_inv _ := by ext; simp
  right_inv _ := by ext; simp

@[simp]
theorem continuousMultilinearMapCongrRight_symm (g : F â‰ƒL[ğ•œ] G) :
    (g.continuousMultilinearMapCongrRight E).symm = g.symm.continuousMultilinearMapCongrRight E :=
  rfl

@[simp]
theorem continuousMultilinearMapCongrRight_apply (g : F â‰ƒL[ğ•œ] G)
    (f : ContinuousMultilinearMap ğ•œ E F) :
    g.continuousMultilinearMapCongrRight E f = (g : F â†’L[ğ•œ] G).compContinuousMultilinearMap f :=
  rfl

end ContinuousLinearEquiv
