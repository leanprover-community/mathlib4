/-
Copyright (c) 2023 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Topology.Algebra.Module.Multilinear.Bounded
import Mathlib.Topology.Algebra.UniformConvergence

/-!
# Topology on continuous multilinear maps

In this file we define `TopologicalSpace` and `UniformSpace` structures
on `ContinuousMultilinearMap ğ•œ E F`,
where `E i` is a family of vector spaces over `ğ•œ` with topologies
ane `F` is a topological vector space.
-/
open Bornology Set
open scoped Topology UniformConvergence Filter

namespace ContinuousMultilinearMap

variable {ğ•œ Î¹ : Type*} {E : Î¹ â†’ Type*} {F : Type*}
  [NormedField ğ•œ]
  [âˆ€ i, TopologicalSpace (E i)] [âˆ€ i, AddCommGroup (E i)] [âˆ€ i, Module ğ•œ (E i)]
  [AddCommGroup F] [Module ğ•œ F]

/-- An auxiliary definition used to define topology on `ContinuousMultilinearMap ğ•œ E F`. -/
def toUniformOnFun [TopologicalSpace F] (f : ContinuousMultilinearMap ğ•œ E F) :
    (âˆ€ i, E i) â†’áµ¤[{s | IsVonNBounded ğ•œ s}] F :=
  UniformOnFun.ofFun _ f

@[simp]
lemma toUniformOnFun_toFun [TopologicalSpace F] (f : ContinuousMultilinearMap ğ•œ E F) :
    UniformOnFun.toFun _ f.toUniformOnFun = f :=
  rfl

instance instUniformSpace [TopologicalSpace F] [TopologicalAddGroup F] :
    UniformSpace (ContinuousMultilinearMap ğ•œ E F) :=
  letI := TopologicalAddGroup.toUniformSpace F
  .comap toUniformOnFun inferInstance

section UniformAddGroup

variable [UniformSpace F] [UniformAddGroup F]

lemma uniformEmbedding_toUniformOnFun :
    UniformEmbedding (toUniformOnFun : ContinuousMultilinearMap ğ•œ E F â†’ _) where
  inj := DFunLike.coe_injective
  comap_uniformity := by rw [uniformity_comap, UniformAddGroup.toUniformSpace_eq]; rfl

lemma embedding_toUniformOnFun : Embedding (toUniformOnFun : ContinuousMultilinearMap ğ•œ E F â†’ _) :=
  uniformEmbedding_toUniformOnFun.embedding

theorem uniformContinuous_coe_fun [âˆ€ i, ContinuousSMul ğ•œ (E i)] :
    UniformContinuous (DFunLike.coe : ContinuousMultilinearMap ğ•œ E F â†’ (âˆ€ i, E i) â†’ F) :=
  (UniformOnFun.uniformContinuous_toFun isVonNBounded_covers).comp
    uniformEmbedding_toUniformOnFun.uniformContinuous

theorem uniformContinuous_eval_left [âˆ€ i, ContinuousSMul ğ•œ (E i)] (x : âˆ€ i, E i) :
    UniformContinuous fun f : ContinuousMultilinearMap ğ•œ E F â†¦ f x :=
  uniformContinuous_pi.1 uniformContinuous_coe_fun x

end UniformAddGroup

variable [TopologicalSpace F] [TopologicalAddGroup F]

instance : UniformAddGroup (ContinuousMultilinearMap ğ•œ E F) :=
  let Ï† : ContinuousMultilinearMap ğ•œ E F â†’+ (âˆ€ i, E i) â†’áµ¤[{s | IsVonNBounded ğ•œ s}] F :=
    { toFun := toUniformOnFun, map_add' := fun _ _ â†¦ rfl, map_zero' := rfl }
  letI := TopologicalAddGroup.toUniformSpace F
  haveI := comm_topologicalAddGroup_is_uniform (G := F)
  uniformEmbedding_toUniformOnFun.uniformAddGroup Ï†

instance {M : Type*} [Monoid M] [DistribMulAction M F] [SMulCommClass ğ•œ M F]
    [ContinuousConstSMul M F] : UniformContinuousConstSMul M (ContinuousMultilinearMap ğ•œ E F) :=
  letI := TopologicalAddGroup.toUniformSpace F
  haveI := comm_topologicalAddGroup_is_uniform (G := F)
  haveI := uniformContinuousConstSMul_of_continuousConstSMul M F
  uniformEmbedding_toUniformOnFun.uniformContinuousConstSMul fun _ _ â†¦ rfl

instance [ContinuousSMul ğ•œ F] : ContinuousSMul ğ•œ (ContinuousMultilinearMap ğ•œ E F) :=
  letI := TopologicalAddGroup.toUniformSpace F
  haveI := comm_topologicalAddGroup_is_uniform (G := F)
  let Ï† : ContinuousMultilinearMap ğ•œ E F â†’â‚—[ğ•œ] (âˆ€ i, E i) â†’ F :=
    { toFun := (â†‘), map_add' := fun _ _ â†¦ rfl, map_smul' := fun _ _ â†¦ rfl }
  UniformOnFun.continuousSMul_induced_of_image_bounded _ _ _ _ Ï†
    embedding_toUniformOnFun.toInducing fun _ _ hu â†¦ hu.image_multilinear _

theorem hasBasis_nhds_zero_of_basis {Î¹ : Type*} {p : Î¹ â†’ Prop} {b : Î¹ â†’ Set F}
    (h : (ğ“ (0 : F)).HasBasis p b) :
    (ğ“ (0 : ContinuousMultilinearMap ğ•œ E F)).HasBasis
      (fun Si : Set (âˆ€ i, E i) Ã— Î¹ => IsVonNBounded ğ•œ Si.1 âˆ§ p Si.2)
      fun Si => { f | MapsTo f Si.1 (b Si.2) } := by
  letI : UniformSpace F := TopologicalAddGroup.toUniformSpace F
  haveI : UniformAddGroup F := comm_topologicalAddGroup_is_uniform
  rw [nhds_induced]
  refine (UniformOnFun.hasBasis_nhds_zero_of_basis _ ?_ ?_ h).comap DFunLike.coe
  Â· exact âŸ¨âˆ…, isVonNBounded_empty _ _âŸ©
  Â· exact directedOn_of_sup_mem fun _ _ => Bornology.IsVonNBounded.union

theorem hasBasis_nhds_zero :
    (ğ“ (0 : ContinuousMultilinearMap ğ•œ E F)).HasBasis
      (fun SV : Set (âˆ€ i, E i) Ã— Set F => IsVonNBounded ğ•œ SV.1 âˆ§ SV.2 âˆˆ ğ“ 0) fun SV =>
      { f | MapsTo f SV.1 SV.2 } :=
  hasBasis_nhds_zero_of_basis (Filter.basis_sets _)

variable [âˆ€ i, ContinuousSMul ğ•œ (E i)]

theorem continuous_eval_left (x : âˆ€ i, E i) :
    Continuous fun p : ContinuousMultilinearMap ğ•œ E F â†¦ p x := by
  letI := TopologicalAddGroup.toUniformSpace F
  haveI := comm_topologicalAddGroup_is_uniform (G := F)
  exact (uniformContinuous_eval_left x).continuous
#align continuous_multilinear_map.continuous_eval_left ContinuousMultilinearMap.continuous_eval_left

theorem continuous_coe_fun :
    Continuous (DFunLike.coe : ContinuousMultilinearMap ğ•œ E F â†’ (âˆ€ i, E i) â†’ F) :=
  continuous_pi continuous_eval_left

instance [T2Space F] : T2Space (ContinuousMultilinearMap ğ•œ E F) :=
  .of_injective_continuous DFunLike.coe_injective continuous_coe_fun

variable (ğ•œ E F)

/-- The application of a multilinear map as a `ContinuousLinearMap`. -/
def apply [ContinuousConstSMul ğ•œ F] (m : âˆ€ i, E i) : ContinuousMultilinearMap ğ•œ E F â†’L[ğ•œ] F where
  toFun c := c m
  map_add' _ _ := rfl
  map_smul' _ _ := rfl
  cont := continuous_eval_left m

variable {ğ•œ E F}

@[simp]
lemma apply_apply [ContinuousConstSMul ğ•œ F] {m : âˆ€ i, E i} {c : ContinuousMultilinearMap ğ•œ E F} :
    (apply ğ•œ E F m) c = c m := rfl

theorem hasSum_eval {Î± : Type*} {p : Î± â†’ ContinuousMultilinearMap ğ•œ E F}
    {q : ContinuousMultilinearMap ğ•œ E F} (h : HasSum p q) (m : âˆ€ i, E i) :
    HasSum (fun a => p a m) (q m) :=
  h.map (applyAddHom m) (continuous_eval_left m)
#align continuous_multilinear_map.has_sum_eval ContinuousMultilinearMap.hasSum_eval

theorem tsum_eval [T2Space F] {Î± : Type*} {p : Î± â†’ ContinuousMultilinearMap ğ•œ E F} (hp : Summable p)
    (m : âˆ€ i, E i) : (âˆ‘' a, p a) m = âˆ‘' a, p a m :=
  (hasSum_eval hp.hasSum m).tsum_eq.symm
#align continuous_multilinear_map.tsum_eval ContinuousMultilinearMap.tsum_eval

end ContinuousMultilinearMap
