/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Anatole Dedecker
-/
import Mathlib.Analysis.LocallyConvex.BalancedCoreHull
import Mathlib.Analysis.Normed.Module.Basic
import Mathlib.LinearAlgebra.FiniteDimensional.Lemmas
import Mathlib.LinearAlgebra.FreeModule.Finite.Matrix
import Mathlib.RingTheory.LocalRing.Basic
import Mathlib.Topology.Algebra.Module.Determinant
import Mathlib.Topology.Algebra.Module.ModuleTopology
import Mathlib.Topology.Algebra.Module.Simple
import Mathlib.Topology.Algebra.SeparationQuotient.FiniteDimensional

/-!
# Finite-dimensional topological vector spaces over complete fields

Let `ğ•œ` be a complete nontrivially normed field, and `E` a topological vector space (TVS) over
`ğ•œ` (i.e we have `[AddCommGroup E] [Module ğ•œ E] [TopologicalSpace E] [IsTopologicalAddGroup E]`
and `[ContinuousSMul ğ•œ E]`).

If `E` is finite dimensional and Hausdorff, then all linear maps from `E` to any other TVS are
continuous.

When `E` is a normed space, this gets us the equivalence of norms in finite dimension.

## Main results :

* `LinearMap.continuous_iff_isClosed_ker` : a linear form is continuous if and only if its kernel
  is closed.
* `LinearMap.continuous_of_finiteDimensional` : a linear map on a finite-dimensional Hausdorff
  space over a complete field is continuous.

## TODO

Generalize more of `Mathlib/Analysis/Normed/Module/FiniteDimension.lean` to general TVSs.

## Implementation detail

The main result from which everything follows is the fact that, if `Î¾ : Î¹ â†’ E` is a finite basis,
then `Î¾.equivFun : E â†’â‚— (Î¹ â†’ ğ•œ)` is continuous. However, for technical reasons, it is easier to
prove this when `Î¹` and `E` live in the same universe. So we start by doing that as a private
lemma, then we deduce `LinearMap.continuous_of_finiteDimensional` from it, and then the general
result follows as `continuous_equivFun_basis`.

-/

open Filter Module Set TopologicalSpace Topology

universe u v w x

noncomputable section

section Field

variable {ğ•œ E F : Type*} [Field ğ•œ] [TopologicalSpace ğ•œ] [AddCommGroup E] [Module ğ•œ E]
  [TopologicalSpace E] [AddCommGroup F] [Module ğ•œ F] [TopologicalSpace F] [IsTopologicalAddGroup F]
  [ContinuousSMul ğ•œ F]

/-- The space of continuous linear maps between finite-dimensional spaces is finite-dimensional. -/
instance [FiniteDimensional ğ•œ E] [FiniteDimensional ğ•œ F] : FiniteDimensional ğ•œ (E â†’L[ğ•œ] F) :=
  FiniteDimensional.of_injective (ContinuousLinearMap.coeLM ğ•œ : (E â†’L[ğ•œ] F) â†’â‚—[ğ•œ] E â†’â‚—[ğ•œ] F)
    ContinuousLinearMap.coe_injective

end Field

section NormedField

variable {ğ•œ : Type u} [hnorm : NontriviallyNormedField ğ•œ] {E : Type v} [AddCommGroup E] [Module ğ•œ E]
  [TopologicalSpace E] [IsTopologicalAddGroup E] [ContinuousSMul ğ•œ E] {F : Type w} [AddCommGroup F]
  [Module ğ•œ F] [TopologicalSpace F] [IsTopologicalAddGroup F] [ContinuousSMul ğ•œ F] {F' : Type x}
  [AddCommGroup F'] [Module ğ•œ F'] [TopologicalSpace F'] [IsTopologicalAddGroup F']
  [ContinuousSMul ğ•œ F']

/-- If `ğ•œ` is a nontrivially normed field, any T2 topology on `ğ•œ` which makes it a topological
vector space over itself (with the norm topology) is *equal* to the norm topology. -/
theorem unique_topology_of_t2 {t : TopologicalSpace ğ•œ} (hâ‚ : @IsTopologicalAddGroup ğ•œ t _)
    (hâ‚‚ : @ContinuousSMul ğ•œ ğ•œ _ hnorm.toUniformSpace.toTopologicalSpace t) (hâ‚ƒ : @T2Space ğ•œ t) :
    t = hnorm.toUniformSpace.toTopologicalSpace := by
  -- Let `ğ“£â‚€` denote the topology on `ğ•œ` induced by the norm, and `ğ“£` be any T2 vector
  -- topology on `ğ•œ`. To show that `ğ“£â‚€ = ğ“£`, it suffices to show that they have the same
  -- neighborhoods of 0.
  refine IsTopologicalAddGroup.ext hâ‚ inferInstance (le_antisymm ?_ ?_)
  Â· -- To show `ğ“£ â‰¤ ğ“£â‚€`, we have to show that closed balls are `ğ“£`-neighborhoods of 0.
    rw [Metric.nhds_basis_closedBall.ge_iff]
    -- Let `Îµ > 0`. Since `ğ•œ` is nontrivially normed, we have `0 < â€–Î¾â‚€â€– < Îµ` for some `Î¾â‚€ : ğ•œ`.
    intro Îµ hÎµ
    rcases NormedField.exists_norm_lt ğ•œ hÎµ with âŸ¨Î¾â‚€, hÎ¾â‚€, hÎ¾â‚€ÎµâŸ©
    -- Since `Î¾â‚€ â‰  0` and `ğ“£` is T2, we know that `{Î¾â‚€}á¶œ` is a `ğ“£`-neighborhood of 0.
    have : {Î¾â‚€}á¶œ âˆˆ @nhds ğ•œ t 0 := IsOpen.mem_nhds isOpen_compl_singleton <|
      mem_compl_singleton_iff.mpr <| Ne.symm <| norm_ne_zero_iff.mp hÎ¾â‚€.ne.symm
    -- Thus, its balanced core `ğ“‘` is too. Let's show that the closed ball of radius `Îµ` contains
    -- `ğ“‘`, which will imply that the closed ball is indeed a `ğ“£`-neighborhood of 0.
    have : balancedCore ğ•œ {Î¾â‚€}á¶œ âˆˆ @nhds ğ•œ t 0 := balancedCore_mem_nhds_zero this
    refine mem_of_superset this fun Î¾ hÎ¾ => ?_
    -- Let `Î¾ âˆˆ ğ“‘`. We want to show `â€–Î¾â€– < Îµ`. If `Î¾ = 0`, this is trivial.
    by_cases hÎ¾0 : Î¾ = 0
    Â· rw [hÎ¾0]
      exact Metric.mem_closedBall_self hÎµ.le
    Â· rw [mem_closedBall_zero_iff]
      -- Now suppose `Î¾ â‰  0`. By contradiction, let's assume `Îµ < â€–Î¾â€–`, and show that
      -- `Î¾â‚€ âˆˆ ğ“‘ âŠ† {Î¾â‚€}á¶œ`, which is a contradiction.
      by_contra! h
      suffices (Î¾â‚€ * Î¾â»Â¹) â€¢ Î¾ âˆˆ balancedCore ğ•œ {Î¾â‚€}á¶œ by
        rw [smul_eq_mul, mul_assoc, inv_mul_cancelâ‚€ hÎ¾0, mul_one] at this
        exact notMem_compl_iff.mpr (mem_singleton Î¾â‚€) ((balancedCore_subset _) this)
      -- For that, we use that `ğ“‘` is balanced : since `â€–Î¾â‚€â€– < Îµ < â€–Î¾â€–`, we have `â€–Î¾â‚€ / Î¾â€– â‰¤ 1`,
      -- hence `Î¾â‚€ = (Î¾â‚€ / Î¾) â€¢ Î¾ âˆˆ ğ“‘` because `Î¾ âˆˆ ğ“‘`.
      refine (balancedCore_balanced _).smul_mem ?_ hÎ¾
      rw [norm_mul, norm_inv, mul_inv_le_iffâ‚€ (norm_pos_iff.mpr hÎ¾0), one_mul]
      exact (hÎ¾â‚€Îµ.trans h).le
  Â· -- Finally, to show `ğ“£â‚€ â‰¤ ğ“£`, we simply argue that `id = (fun x â†¦ x â€¢ 1)` is continuous from
    -- `(ğ•œ, ğ“£â‚€)` to `(ğ•œ, ğ“£)` because `(â€¢) : (ğ•œ, ğ“£â‚€) Ã— (ğ•œ, ğ“£) â†’ (ğ•œ, ğ“£)` is continuous.
    calc
      @nhds ğ•œ hnorm.toUniformSpace.toTopologicalSpace 0 =
          map id (@nhds ğ•œ hnorm.toUniformSpace.toTopologicalSpace 0) :=
        map_id.symm
      _ = map (fun x => id x â€¢ (1 : ğ•œ)) (@nhds ğ•œ hnorm.toUniformSpace.toTopologicalSpace 0) := by
        conv_rhs =>
          congr
          ext
          rw [smul_eq_mul, mul_one]
      _ â‰¤ @nhds ğ•œ t ((0 : ğ•œ) â€¢ (1 : ğ•œ)) :=
        (@Tendsto.smul_const _ _ _ hnorm.toUniformSpace.toTopologicalSpace t _ _ _ _ _
          tendsto_id (1 : ğ•œ))
      _ = @nhds ğ•œ t 0 := by rw [zero_smul]

/-- Any linear form on a topological vector space over a nontrivially normed field is continuous if
its kernel is closed. -/
theorem LinearMap.continuous_of_isClosed_ker (l : E â†’â‚—[ğ•œ] ğ•œ)
    (hl : IsClosed (LinearMap.ker l : Set E)) :
    Continuous l := by
  -- `l` is either constant or surjective. If it is constant, the result is trivial.
  by_cases H : finrank ğ•œ (LinearMap.range l) = 0
  Â· rw [Submodule.finrank_eq_zero, LinearMap.range_eq_bot] at H
    rw [H]
    exact continuous_zero
  Â· -- In the case where `l` is surjective, we factor it as `Ï† : (E â§¸ l.ker) â‰ƒâ‚—[ğ•œ] ğ•œ`. Note that
    -- `E â§¸ l.ker` is T2 since `l.ker` is closed.
    have : finrank ğ•œ (LinearMap.range l) = 1 :=
      le_antisymm (finrank_self ğ•œ â–¸ (LinearMap.range l).finrank_le) (zero_lt_iff.mpr H)
    have hi : Function.Injective ((LinearMap.ker l).liftQ l (le_refl _)) := by
      rw [â† LinearMap.ker_eq_bot]
      exact Submodule.ker_liftQ_eq_bot _ _ _ (le_refl _)
    have hs : Function.Surjective ((LinearMap.ker l).liftQ l (le_refl _)) := by
      rw [â† LinearMap.range_eq_top, Submodule.range_liftQ]
      exact Submodule.eq_top_of_finrank_eq ((finrank_self ğ•œ).symm â–¸ this)
    let Ï† : (E â§¸ LinearMap.ker l) â‰ƒâ‚—[ğ•œ] ğ•œ :=
      LinearEquiv.ofBijective ((LinearMap.ker l).liftQ l (le_refl _)) âŸ¨hi, hsâŸ©
    have hlÏ† : (l : E â†’ ğ•œ) = Ï† âˆ˜ (LinearMap.ker l).mkQ := by ext; rfl
    -- Since the quotient map `E â†’â‚—[ğ•œ] (E â§¸ l.ker)` is continuous, the continuity of `l` will follow
    -- form the continuity of `Ï†`.
    suffices Continuous Ï†.toEquiv by
      rw [hlÏ†]
      exact this.comp continuous_quot_mk
    -- The pullback by `Ï†.symm` of the quotient topology is a T2 topology on `ğ•œ`, because `Ï†.symm`
    -- is injective. Since `Ï†.symm` is linear, it is also a vector space topology.
    -- Hence, we know that it is equal to the topology induced by the norm.
    have : induced Ï†.toEquiv.symm inferInstance = hnorm.toUniformSpace.toTopologicalSpace := by
      refine unique_topology_of_t2 (topologicalAddGroup_induced Ï†.symm.toLinearMap)
        (continuousSMul_induced Ï†.symm.toMulActionHom) ?_
      rw [t2Space_iff]
      exact fun x y hxy =>
        @separated_by_continuous _ _ (induced _ _) _ _ _ continuous_induced_dom _ _
          (Ï†.toEquiv.symm.injective.ne hxy)
    -- Finally, the pullback by `Ï†.symm` is exactly the pushforward by `Ï†`, so we have to prove
    -- that `Ï†` is continuous when `ğ•œ` is endowed with the pushforward by `Ï†` of the quotient
    -- topology, which is trivial by definition of the pushforward.
    simp_rw [this.symm, Equiv.induced_symm]
    exact continuous_coinduced_rng

/-- Any linear form on a topological vector space over a nontrivially normed field is continuous if
and only if its kernel is closed. -/
theorem LinearMap.continuous_iff_isClosed_ker (l : E â†’â‚—[ğ•œ] ğ•œ) :
    Continuous l â†” IsClosed (LinearMap.ker l : Set E) :=
  âŸ¨fun h => isClosed_singleton.preimage h, l.continuous_of_isClosed_kerâŸ©

/-- Over a nontrivially normed field, any linear form which is nonzero on a nonempty open set is
automatically continuous. -/
theorem LinearMap.continuous_of_nonzero_on_open (l : E â†’â‚—[ğ•œ] ğ•œ) (s : Set E) (hsâ‚ : IsOpen s)
    (hsâ‚‚ : s.Nonempty) (hsâ‚ƒ : âˆ€ x âˆˆ s, l x â‰  0) : Continuous l := by
  refine l.continuous_of_isClosed_ker (l.isClosed_or_dense_ker.resolve_right fun hl => ?_)
  rcases hsâ‚‚ with âŸ¨x, hxâŸ©
  have : x âˆˆ interior (LinearMap.ker l : Set E)á¶œ := by
    rw [mem_interior_iff_mem_nhds]
    exact mem_of_superset (hsâ‚.mem_nhds hx) hsâ‚ƒ
  rwa [hl.interior_compl] at this

variable [CompleteSpace ğ•œ]

/-- This version imposes `Î¹` and `E` to live in the same universe, so you should instead use
`continuous_equivFun_basis` which gives the same result without universe restrictions. -/
private theorem continuous_equivFun_basis_aux [T2Space E] {Î¹ : Type v} [Fintype Î¹]
    (Î¾ : Basis Î¹ ğ•œ E) : Continuous Î¾.equivFun := by
  letI : UniformSpace E := IsTopologicalAddGroup.toUniformSpace E
  letI : IsUniformAddGroup E := isUniformAddGroup_of_addCommGroup
  suffices âˆ€ n, Fintype.card Î¹ = n â†’ Continuous Î¾.equivFun by exact this _ rfl
  intro n hn
  induction n generalizing Î¹ E with
  | zero =>
    rw [Fintype.card_eq_zero_iff] at hn
    exact continuous_of_const fun x y => funext hn.elim
  | succ n IH =>
    haveI : FiniteDimensional ğ•œ E := .of_fintype_basis Î¾
    -- first step: thanks to the induction hypothesis, any n-dimensional subspace is equivalent
    -- to a standard space of dimension n, hence it is complete and therefore closed.
    have Hâ‚ : âˆ€ s : Submodule ğ•œ E, finrank ğ•œ s = n â†’ IsClosed (s : Set E) := by
      intro s s_dim
      letI : IsUniformAddGroup s := s.toAddSubgroup.isUniformAddGroup
      let b := Basis.ofVectorSpace ğ•œ s
      have U : IsUniformEmbedding b.equivFun.symm.toEquiv := by
        have : Fintype.card (Basis.ofVectorSpaceIndex ğ•œ s) = n := by
          rw [â† s_dim]
          exact (finrank_eq_card_basis b).symm
        have : Continuous b.equivFun := IH b this
        exact
          b.equivFun.symm.isUniformEmbedding b.equivFun.symm.toLinearMap.continuous_on_pi this
      have : IsComplete (s : Set E) :=
        completeSpace_coe_iff_isComplete.1 ((completeSpace_congr U).1 inferInstance)
      exact this.isClosed
    -- second step: any linear form is continuous, as its kernel is closed by the first step
    have Hâ‚‚ : âˆ€ f : E â†’â‚—[ğ•œ] ğ•œ, Continuous f := by
      intro f
      by_cases H : finrank ğ•œ (LinearMap.range f) = 0
      Â· rw [Submodule.finrank_eq_zero, LinearMap.range_eq_bot] at H
        rw [H]
        exact continuous_zero
      Â· have : finrank ğ•œ (LinearMap.ker f) = n := by
          have Z := f.finrank_range_add_finrank_ker
          rw [finrank_eq_card_basis Î¾, hn] at Z
          have : finrank ğ•œ (LinearMap.range f) = 1 :=
            le_antisymm (finrank_self ğ•œ â–¸ (LinearMap.range f).finrank_le) (zero_lt_iff.mpr H)
          rw [this, add_comm, Nat.add_one] at Z
          exact Nat.succ.inj Z
        have : IsClosed (LinearMap.ker f : Set E) := Hâ‚ _ this
        exact LinearMap.continuous_of_isClosed_ker f this
    rw [continuous_pi_iff]
    intro i
    change Continuous (Î¾.coord i)
    exact Hâ‚‚ (Î¾.coord i)

/-- A finite-dimensional t2 vector space over a complete field must carry the module topology.

Not declared as a global instance only for performance reasons. -/
@[local instance]
lemma isModuleTopologyOfFiniteDimensional [T2Space E] [FiniteDimensional ğ•œ E] :
    IsModuleTopology ğ•œ E :=
  -- for the proof, go to a model vector space `b â†’ ğ•œ` thanks to `continuous_equivFun_basis`, and
  -- use that it has the module topology
  let b := Basis.ofVectorSpace ğ•œ E
  have continuousEquiv : E â‰ƒL[ğ•œ] (Basis.ofVectorSpaceIndex ğ•œ E) â†’ ğ•œ :=
    { __ := b.equivFun
      continuous_toFun := continuous_equivFun_basis_aux b
      continuous_invFun := IsModuleTopology.continuous_of_linearMap (R := ğ•œ)
        (A := (Basis.ofVectorSpaceIndex ğ•œ E) â†’ ğ•œ) (B := E) b.equivFun.symm }
  IsModuleTopology.iso continuousEquiv.symm

/-- Any linear map on a finite-dimensional space over a complete field is continuous. -/
theorem LinearMap.continuous_of_finiteDimensional [T2Space E] [FiniteDimensional ğ•œ E]
    (f : E â†’â‚—[ğ•œ] F') : Continuous f :=
  IsModuleTopology.continuous_of_linearMap f

instance LinearMap.continuousLinearMapClassOfFiniteDimensional [T2Space E] [FiniteDimensional ğ•œ E] :
    ContinuousLinearMapClass (E â†’â‚—[ğ•œ] F') ğ•œ E F' :=
  { LinearMap.semilinearMapClass with map_continuous := fun f => f.continuous_of_finiteDimensional }

/-- In finite dimensions over a non-discrete complete normed field, the canonical identification
(in terms of a basis) with `ğ•œ^n` (endowed with the product topology) is continuous.
This is the key fact which makes all linear maps from a T2 finite-dimensional TVS over such a field
continuous (see `LinearMap.continuous_of_finiteDimensional`), which in turn implies that all
norms are equivalent in finite dimensions. -/
theorem continuous_equivFun_basis [T2Space E] {Î¹ : Type*} [Finite Î¹] (Î¾ : Basis Î¹ ğ•œ E) :
    Continuous Î¾.equivFun :=
  haveI : FiniteDimensional ğ•œ E := .of_fintype_basis Î¾
  Î¾.equivFun.toLinearMap.continuous_of_finiteDimensional

namespace LinearMap

variable [T2Space E] [FiniteDimensional ğ•œ E]

/-- The continuous linear map induced by a linear map on a finite-dimensional space -/
def toContinuousLinearMap : (E â†’â‚—[ğ•œ] F') â‰ƒâ‚—[ğ•œ] E â†’L[ğ•œ] F' where
  toFun f := âŸ¨f, f.continuous_of_finiteDimensionalâŸ©
  invFun := (â†‘)
  map_add' _ _ := rfl
  map_smul' _ _ := rfl
  right_inv _ := ContinuousLinearMap.coe_injective rfl

/-- Algebra equivalence between the linear maps and continuous linear maps on a finite-dimensional
space. -/
def _root_.Module.End.toContinuousLinearMap (E : Type v) [NormedAddCommGroup E]
    [NormedSpace ğ•œ E] [FiniteDimensional ğ•œ E] : (E â†’â‚—[ğ•œ] E) â‰ƒâ‚[ğ•œ] (E â†’L[ğ•œ] E) :=
  { LinearMap.toContinuousLinearMap with
    map_mul' := fun _ _ â†¦ rfl
    commutes' := fun _ â†¦ rfl }

@[simp]
theorem coe_toContinuousLinearMap' (f : E â†’â‚—[ğ•œ] F') : â‡‘(LinearMap.toContinuousLinearMap f) = f :=
  rfl

@[simp]
theorem coe_toContinuousLinearMap (f : E â†’â‚—[ğ•œ] F') :
    ((LinearMap.toContinuousLinearMap f) : E â†’â‚—[ğ•œ] F') = f :=
  rfl

@[simp]
theorem coe_toContinuousLinearMap_symm :
    â‡‘(toContinuousLinearMap : (E â†’â‚—[ğ•œ] F') â‰ƒâ‚—[ğ•œ] E â†’L[ğ•œ] F').symm =
      ((â†‘) : (E â†’L[ğ•œ] F') â†’ E â†’â‚—[ğ•œ] F') :=
  rfl

@[simp]
theorem det_toContinuousLinearMap (f : E â†’â‚—[ğ•œ] E) :
    (LinearMap.toContinuousLinearMap f).det = LinearMap.det f :=
  rfl

@[simp]
theorem ker_toContinuousLinearMap (f : E â†’â‚—[ğ•œ] F') :
    ker (LinearMap.toContinuousLinearMap f) = ker f :=
  rfl

@[simp]
theorem range_toContinuousLinearMap (f : E â†’â‚—[ğ•œ] F') :
    range (LinearMap.toContinuousLinearMap f) = range f :=
  rfl

/-- A surjective linear map `f` with finite-dimensional codomain is an open map. -/
theorem isOpenMap_of_finiteDimensional (f : F â†’â‚—[ğ•œ] E) (hf : Function.Surjective f) :
    IsOpenMap f :=
  IsModuleTopology.isOpenMap_of_surjective hf

instance canLiftContinuousLinearMap : CanLift (E â†’â‚—[ğ•œ] F) (E â†’L[ğ•œ] F) (â†‘) fun _ => True :=
  âŸ¨fun f _ => âŸ¨LinearMap.toContinuousLinearMap f, rflâŸ©âŸ©

lemma toContinuousLinearMap_eq_iff_eq_toLinearMap (f : E â†’â‚—[ğ•œ] E) (g : E â†’L[ğ•œ] E) :
    f.toContinuousLinearMap = g â†” f = g.toLinearMap := by
  simp [ContinuousLinearMap.ext_iff, LinearMap.ext_iff]

lemma _root_.ContinuousLinearMap.toLinearMap_eq_iff_eq_toContinuousLinearMap (g : E â†’L[ğ•œ] E)
    (f : E â†’â‚—[ğ•œ] E) : g.toLinearMap = f â†” g = f.toContinuousLinearMap := by
  simp [ContinuousLinearMap.ext_iff, LinearMap.ext_iff]

end LinearMap

section

variable [T2Space E] [T2Space F] [FiniteDimensional ğ•œ E]

namespace LinearEquiv

/-- The continuous linear equivalence induced by a linear equivalence on a finite-dimensional
space. -/
def toContinuousLinearEquiv (e : E â‰ƒâ‚—[ğ•œ] F) : E â‰ƒL[ğ•œ] F :=
  { e with
    continuous_toFun := e.toLinearMap.continuous_of_finiteDimensional
    continuous_invFun :=
      haveI : FiniteDimensional ğ•œ F := e.finiteDimensional
      e.symm.toLinearMap.continuous_of_finiteDimensional }

@[simp]
theorem coe_toContinuousLinearEquiv (e : E â‰ƒâ‚—[ğ•œ] F) : (e.toContinuousLinearEquiv : E â†’â‚—[ğ•œ] F) = e :=
  rfl

@[simp]
theorem coe_toContinuousLinearEquiv' (e : E â‰ƒâ‚—[ğ•œ] F) : (e.toContinuousLinearEquiv : E â†’ F) = e :=
  rfl

@[simp]
theorem coe_toContinuousLinearEquiv_symm (e : E â‰ƒâ‚—[ğ•œ] F) :
    (e.toContinuousLinearEquiv.symm : F â†’â‚—[ğ•œ] E) = e.symm :=
  rfl

@[simp]
theorem coe_toContinuousLinearEquiv_symm' (e : E â‰ƒâ‚—[ğ•œ] F) :
    (e.toContinuousLinearEquiv.symm : F â†’ E) = e.symm :=
  rfl

@[simp]
theorem toLinearEquiv_toContinuousLinearEquiv (e : E â‰ƒâ‚—[ğ•œ] F) :
    e.toContinuousLinearEquiv.toLinearEquiv = e := by
  ext x
  rfl

theorem toLinearEquiv_toContinuousLinearEquiv_symm (e : E â‰ƒâ‚—[ğ•œ] F) :
    e.toContinuousLinearEquiv.symm.toLinearEquiv = e.symm := by
  ext x
  rfl

instance canLiftContinuousLinearEquiv :
    CanLift (E â‰ƒâ‚—[ğ•œ] F) (E â‰ƒL[ğ•œ] F) ContinuousLinearEquiv.toLinearEquiv fun _ => True :=
  âŸ¨fun f _ => âŸ¨_, f.toLinearEquiv_toContinuousLinearEquivâŸ©âŸ©

end LinearEquiv

variable [FiniteDimensional ğ•œ F]

/-- Two finite-dimensional topological vector spaces over a complete normed field are continuously
linearly equivalent if they have the same (finite) dimension. -/
theorem FiniteDimensional.nonempty_continuousLinearEquiv_of_finrank_eq
    (cond : finrank ğ•œ E = finrank ğ•œ F) : Nonempty (E â‰ƒL[ğ•œ] F) :=
  (nonempty_linearEquiv_of_finrank_eq cond).map LinearEquiv.toContinuousLinearEquiv

/-- Two finite-dimensional topological vector spaces over a complete normed field are continuously
linearly equivalent if and only if they have the same (finite) dimension. -/
theorem FiniteDimensional.nonempty_continuousLinearEquiv_iff_finrank_eq :
    Nonempty (E â‰ƒL[ğ•œ] F) â†” finrank ğ•œ E = finrank ğ•œ F :=
  âŸ¨fun âŸ¨hâŸ© => h.toLinearEquiv.finrank_eq, fun h =>
    FiniteDimensional.nonempty_continuousLinearEquiv_of_finrank_eq hâŸ©

/-- A continuous linear equivalence between two finite-dimensional topological vector spaces over a
complete normed field of the same (finite) dimension. -/
def ContinuousLinearEquiv.ofFinrankEq (cond : finrank ğ•œ E = finrank ğ•œ F) : E â‰ƒL[ğ•œ] F :=
  (LinearEquiv.ofFinrankEq E F cond).toContinuousLinearEquiv

end

namespace Module.Basis
variable {Î¹ : Type*} [Finite Î¹] [T2Space E]

/-- Construct a continuous linear map given the value at a finite basis. -/
def constrL (v : Basis Î¹ ğ•œ E) (f : Î¹ â†’ F) : E â†’L[ğ•œ] F :=
  haveI : FiniteDimensional ğ•œ E := FiniteDimensional.of_fintype_basis v
  LinearMap.toContinuousLinearMap (v.constr ğ•œ f)

@[simp]
theorem coe_constrL (v : Basis Î¹ ğ•œ E) (f : Î¹ â†’ F) : (v.constrL f : E â†’â‚—[ğ•œ] F) = v.constr ğ•œ f :=
  rfl

/-- The continuous linear equivalence between a vector space over `ğ•œ` with a finite basis and
functions from its basis indexing type to `ğ•œ`. -/
@[simps! apply]
def equivFunL (v : Basis Î¹ ğ•œ E) : E â‰ƒL[ğ•œ] Î¹ â†’ ğ•œ :=
  { v.equivFun with
    continuous_toFun :=
      haveI : FiniteDimensional ğ•œ E := FiniteDimensional.of_fintype_basis v
      v.equivFun.toLinearMap.continuous_of_finiteDimensional
    continuous_invFun := by
      change Continuous v.equivFun.symm.toFun
      exact v.equivFun.symm.toLinearMap.continuous_of_finiteDimensional }

@[simp]
lemma equivFunL_symm_apply_repr (v : Basis Î¹ ğ•œ E) (x : E) :
    v.equivFunL.symm (v.repr x) = x :=
  v.equivFunL.symm_apply_apply x

@[simp]
theorem constrL_apply {Î¹ : Type*} [Fintype Î¹] (v : Basis Î¹ ğ•œ E) (f : Î¹ â†’ F) (e : E) :
    v.constrL f e = âˆ‘ i, v.equivFun e i â€¢ f i :=
  v.constr_apply_fintype ğ•œ _ _

@[simp 1100]
theorem constrL_basis (v : Basis Î¹ ğ•œ E) (f : Î¹ â†’ F) (i : Î¹) : v.constrL f (v i) = f i :=
  v.constr_basis ğ•œ _ _

end Module.Basis

namespace ContinuousLinearMap

variable [T2Space E] [FiniteDimensional ğ•œ E]

/-- Builds a continuous linear equivalence from a continuous linear map on a finite-dimensional
vector space whose determinant is nonzero. -/
def toContinuousLinearEquivOfDetNeZero (f : E â†’L[ğ•œ] E) (hf : f.det â‰  0) : E â‰ƒL[ğ•œ] E :=
  ((f : E â†’â‚—[ğ•œ] E).equivOfDetNeZero hf).toContinuousLinearEquiv

@[simp]
theorem coe_toContinuousLinearEquivOfDetNeZero (f : E â†’L[ğ•œ] E) (hf : f.det â‰  0) :
    (f.toContinuousLinearEquivOfDetNeZero hf : E â†’L[ğ•œ] E) = f := by
  ext x
  rfl

@[simp]
theorem toContinuousLinearEquivOfDetNeZero_apply (f : E â†’L[ğ•œ] E) (hf : f.det â‰  0) (x : E) :
    f.toContinuousLinearEquivOfDetNeZero hf x = f x :=
  rfl

theorem _root_.Matrix.toLin_finTwoProd_toContinuousLinearMap (a b c d : ğ•œ) :
    LinearMap.toContinuousLinearMap
      (Matrix.toLin (Basis.finTwoProd ğ•œ) (Basis.finTwoProd ğ•œ) !![a, b; c, d]) =
      (a â€¢ ContinuousLinearMap.fst ğ•œ ğ•œ ğ•œ + b â€¢ ContinuousLinearMap.snd ğ•œ ğ•œ ğ•œ).prod
        (c â€¢ ContinuousLinearMap.fst ğ•œ ğ•œ ğ•œ + d â€¢ ContinuousLinearMap.snd ğ•œ ğ•œ ğ•œ) :=
  ContinuousLinearMap.ext <| Matrix.toLin_finTwoProd_apply _ _ _ _

end ContinuousLinearMap

end NormedField

section IsUniformAddGroup

variable (ğ•œ E : Type*) [NontriviallyNormedField ğ•œ]
  [CompleteSpace ğ•œ] [AddCommGroup E] [UniformSpace E] [T2Space E] [IsUniformAddGroup E]
  [Module ğ•œ E] [ContinuousSMul ğ•œ E]

include ğ•œ in
theorem FiniteDimensional.complete [FiniteDimensional ğ•œ E] : CompleteSpace E := by
  set e := ContinuousLinearEquiv.ofFinrankEq (@finrank_fin_fun ğ•œ _ _ (finrank ğ•œ E)).symm
  have : IsUniformEmbedding e.toEquiv.symm := e.symm.isUniformEmbedding
  exact (completeSpace_congr this).1 inferInstance

variable {ğ•œ E}

/-- A finite-dimensional subspace is complete. -/
theorem Submodule.complete_of_finiteDimensional (s : Submodule ğ•œ E) [FiniteDimensional ğ•œ s] :
    IsComplete (s : Set E) :=
  haveI : IsUniformAddGroup s := s.toAddSubgroup.isUniformAddGroup
  completeSpace_coe_iff_isComplete.1 (FiniteDimensional.complete ğ•œ s)

end IsUniformAddGroup

variable {ğ•œ E F : Type*} [NontriviallyNormedField ğ•œ] [CompleteSpace ğ•œ]
  [AddCommGroup E] [TopologicalSpace E] [IsTopologicalAddGroup E] [Module ğ•œ E]
  [ContinuousSMul ğ•œ E]
  [AddCommGroup F] [TopologicalSpace F] [T2Space F] [IsTopologicalAddGroup F] [Module ğ•œ F]
  [ContinuousSMul ğ•œ F]

/-- A finite-dimensional subspace is closed. -/
theorem Submodule.closed_of_finiteDimensional
    [T2Space E] (s : Submodule ğ•œ E) [FiniteDimensional ğ•œ s] :
    IsClosed (s : Set E) :=
  letI := IsTopologicalAddGroup.toUniformSpace E
  haveI : IsUniformAddGroup E := isUniformAddGroup_of_addCommGroup
  s.complete_of_finiteDimensional.isClosed

/-- An injective linear map with finite-dimensional domain is a closed embedding. -/
theorem LinearMap.isClosedEmbedding_of_injective [T2Space E] [FiniteDimensional ğ•œ E] {f : E â†’â‚—[ğ•œ] F}
    (hf : LinearMap.ker f = âŠ¥) : IsClosedEmbedding f :=
  let g := LinearEquiv.ofInjective f (LinearMap.ker_eq_bot.mp hf)
  { IsEmbedding.subtypeVal.comp g.toContinuousLinearEquiv.toHomeomorph.isEmbedding with
    isClosed_range := by
      haveI := f.finiteDimensional_range
      simpa [LinearMap.coe_range f] using (LinearMap.range f).closed_of_finiteDimensional }

theorem isClosedEmbedding_smul_left [T2Space E] {c : E} (hc : c â‰  0) :
    IsClosedEmbedding fun x : ğ•œ => x â€¢ c :=
  LinearMap.isClosedEmbedding_of_injective (LinearMap.ker_toSpanSingleton ğ•œ E hc)

-- `smul` is a closed map in the first argument.
theorem isClosedMap_smul_left [T2Space E] (c : E) : IsClosedMap fun x : ğ•œ => x â€¢ c := by
  by_cases hc : c = 0
  Â· simp_rw [hc, smul_zero]
    exact isClosedMap_const
  Â· exact (isClosedEmbedding_smul_left hc).isClosedMap

theorem ContinuousLinearMap.exists_right_inverse_of_surjective [FiniteDimensional ğ•œ F]
    (f : E â†’L[ğ•œ] F) (hf : LinearMap.range f = âŠ¤) :
    âˆƒ g : F â†’L[ğ•œ] E, f.comp g = ContinuousLinearMap.id ğ•œ F :=
  let âŸ¨g, hgâŸ© := (f : E â†’â‚—[ğ•œ] F).exists_rightInverse_of_surjective hf
  âŸ¨LinearMap.toContinuousLinearMap g, ContinuousLinearMap.coe_inj.1 hgâŸ©

/-- If `K` is a complete field and `V` is a finite-dimensional vector space over `K` (equipped with
any topology so that `V` is a topological `K`-module, meaning `[IsTopologicalAddGroup V]`
and `[ContinuousSMul K V]`), and `K` is locally compact, then `V` is locally compact.

This is not an instance because `K` cannot be inferred. -/
theorem LocallyCompactSpace.of_finiteDimensional_of_complete (K V : Type*)
    [NontriviallyNormedField K] [CompleteSpace K] [LocallyCompactSpace K]
    [AddCommGroup V] [TopologicalSpace V] [IsTopologicalAddGroup V]
    [Module K V] [ContinuousSMul K V] [FiniteDimensional K V] :
    LocallyCompactSpace V :=
  -- Reduce to `SeparationQuotient V`, which is a `T2Space`.
  suffices LocallyCompactSpace (SeparationQuotient V) from
    SeparationQuotient.isInducing_mk.locallyCompactSpace <|
      SeparationQuotient.range_mk (X := V) â–¸ isClosed_univ.isLocallyClosed
  let âŸ¨_, âŸ¨bâŸ©âŸ© := Basis.exists_basis K (SeparationQuotient V)
  have := FiniteDimensional.fintypeBasisIndex b
  b.equivFun.toContinuousLinearEquiv.toHomeomorph.isOpenEmbedding.locallyCompactSpace
