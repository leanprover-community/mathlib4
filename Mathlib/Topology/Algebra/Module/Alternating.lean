/-
Copyright (c) 2023 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Heather Macbeth, SÃ©bastien GouÃ«zel
-/
import Mathlib.LinearAlgebra.Alternating
import Mathlib.Topology.Algebra.Module.Multilinear

/-!
# Continuous alternating multilinear maps

In this file we define bundled continuous alternating maps and develop basic API about these
maps, by reusing API about continuous multilinear maps and alternating maps.

## Notation

`M [Î›^Î¹]â†’L[R] N`: notation for `R`-linear continuous alternating maps from `M` to `N`; the arguments
are indexed by `i : Î¹`.

## Keywords

multilinear map, alternating map, continuous
-/

open Function Matrix

open scoped BigOperators

/-- A continuous alternating map is a continuous map from `Î¹ â†’ M` to `N` that is

- multilinear : `f (update m i (c â€¢ x)) = c â€¢ f (update m i x)` and
  `f (update m i (x + y)) = f (update m i x) + f (update m i y)`;
- alternating : `f v = 0` whenever `v` has two equal coordinates.
-/
structure ContinuousAlternatingMap (R M N Î¹ : Type*) [Semiring R] [AddCommMonoid M] [Module R M]
    [TopologicalSpace M] [AddCommMonoid N] [Module R N] [TopologicalSpace N] extends
    ContinuousMultilinearMap R (fun _ : Î¹ => M) N, AlternatingMap R M N Î¹ where

/-- Projection to `ContinuousMultilinearMap`s. -/
add_decl_doc ContinuousAlternatingMap.toContinuousMultilinearMap

/-- Projection to `AlternatingMap`s. -/
add_decl_doc ContinuousAlternatingMap.toAlternatingMap

notation M "[Î›^" Î¹ "]â†’L[" R "]" N:100 => ContinuousAlternatingMap R M N Î¹

namespace ContinuousAlternatingMap

section Semiring

variable {R M M' N N' Î¹ : Type*} [Semiring R] [AddCommMonoid M] [Module R M] [TopologicalSpace M]
  [AddCommMonoid M'] [Module R M'] [TopologicalSpace M'] [AddCommMonoid N] [Module R N]
  [TopologicalSpace N] [AddCommMonoid N'] [Module R N'] [TopologicalSpace N'] {n : â„•}
  (f g : M [Î›^Î¹]â†’L[R] N)

theorem toContinuousMultilinearMap_injective :
    Injective (ContinuousAlternatingMap.toContinuousMultilinearMap :
      M [Î›^Î¹]â†’L[R] N â†’ ContinuousMultilinearMap R (fun _ : Î¹ => M) N)
  | âŸ¨_, _âŸ©, âŸ¨_, _âŸ©, rfl => rfl

theorem range_toContinuousMultilinearMap :
    Set.range
        (toContinuousMultilinearMap :
          M [Î›^Î¹]â†’L[R] N â†’ ContinuousMultilinearMap R (fun _ : Î¹ => M) N) =
      {f | âˆ€ (v : Î¹ â†’ M) (i j : Î¹), v i = v j â†’ i â‰  j â†’ f v = 0} :=
  Set.ext fun f => âŸ¨fun âŸ¨g, hgâŸ© => hg â–¸ g.2, fun h => âŸ¨âŸ¨f, hâŸ©, rflâŸ©âŸ©

instance continuousMapClass : ContinuousMapClass (M [Î›^Î¹]â†’L[R] N) (Î¹ â†’ M) N where
  coe f := f.toFun
  coe_injective' _ _ h := toContinuousMultilinearMap_injective <| FunLike.ext' h
  map_continuous f := f.cont

initialize_simps_projections ContinuousAlternatingMap (toFun â†’ apply)

@[continuity]
theorem coe_continuous : Continuous f := f.cont

@[simp]
theorem coe_toContinuousMultilinearMap : â‡‘f.toContinuousMultilinearMap = f :=
  rfl

@[simp]
theorem coe_mk (f : ContinuousMultilinearMap R (fun _ : Î¹ => M) N) (h) : â‡‘(mk f h) = f :=
  rfl

-- not a `simp` lemma because this projection is a reducible call to `mk`, so `simp` can prove
-- this lemma
theorem coe_toAlternatingMap : â‡‘f.toAlternatingMap = f := rfl

@[ext]
theorem ext {f g : M [Î›^Î¹]â†’L[R] N} (H : âˆ€ x, f x = g x) : f = g :=
  FunLike.ext _ _ H

theorem ext_iff {f g : M [Î›^Î¹]â†’L[R] N} : f = g â†” âˆ€ x, f x = g x :=
  FunLike.ext_iff

theorem toAlternatingMap_injective :
    Injective (toAlternatingMap : M [Î›^Î¹]â†’L[R] N â†’ AlternatingMap R M N Î¹) := fun f g h =>
  FunLike.ext' <| by convert FunLike.ext'_iff.1 h
                     -- ðŸŽ‰ no goals

@[simp]
theorem range_toAlternatingMap :
    Set.range (toAlternatingMap : M [Î›^Î¹]â†’L[R] N â†’ AlternatingMap R M N Î¹) =
      {f : AlternatingMap R M N Î¹ | Continuous f} :=
  Set.ext fun f => âŸ¨fun âŸ¨g, hgâŸ© => hg â–¸ g.cont, fun h => âŸ¨{ f with cont := h }, FunLike.ext' rflâŸ©âŸ©

@[simp]
theorem map_add [DecidableEq Î¹] (m : Î¹ â†’ M) (i : Î¹) (x y : M) :
    f (update m i (x + y)) = f (update m i x) + f (update m i y) :=
  f.map_add' m i x y

@[simp]
theorem map_smul [DecidableEq Î¹] (m : Î¹ â†’ M) (i : Î¹) (c : R) (x : M) :
    f (update m i (c â€¢ x)) = c â€¢ f (update m i x) :=
  f.map_smul' m i c x

theorem map_coord_zero {m : Î¹ â†’ M} (i : Î¹) (h : m i = 0) : f m = 0 :=
  f.toMultilinearMap.map_coord_zero i h

@[simp]
theorem map_update_zero [DecidableEq Î¹] (m : Î¹ â†’ M) (i : Î¹) : f (update m i 0) = 0 :=
  f.toMultilinearMap.map_update_zero m i

@[simp]
theorem map_zero [Nonempty Î¹] : f 0 = 0 :=
  f.toMultilinearMap.map_zero

theorem map_eq_zero_of_eq (v : Î¹ â†’ M) {i j : Î¹} (h : v i = v j) (hij : i â‰  j) : f v = 0 :=
  f.map_eq_zero_of_eq' v i j h hij

theorem map_eq_zero_of_not_injective (v : Î¹ â†’ M) (hv : Â¬Function.Injective v) : f v = 0 :=
  f.toAlternatingMap.map_eq_zero_of_not_injective v hv

/-- Restrict the codomain of a continuous alternating map to a submodule. -/
@[simps!]
def codRestrict (f : M [Î›^Î¹]â†’L[R] N) (p : Submodule R N) (h : âˆ€ v, f v âˆˆ p) : M [Î›^Î¹]â†’L[R] p :=
  { f.toAlternatingMap.codRestrict p h with toContinuousMultilinearMap := f.1.codRestrict p h }

instance : Zero (M [Î›^Î¹]â†’L[R] N) :=
  âŸ¨âŸ¨0, (0 : AlternatingMap R M N Î¹).map_eq_zero_of_eqâŸ©âŸ©

instance : Inhabited (M [Î›^Î¹]â†’L[R] N) :=
  âŸ¨0âŸ©

@[simp]
theorem coe_zero : â‡‘(0 : M [Î›^Î¹]â†’L[R] N) = 0 :=
  rfl

@[simp]
theorem toContinuousMultilinearMap_zero : (0 : M [Î›^Î¹]â†’L[R] N).toContinuousMultilinearMap = 0 :=
  rfl

@[simp]
theorem toAlternatingMap_zero : (0 : M [Î›^Î¹]â†’L[R] N).toAlternatingMap = 0 :=
  rfl

section SMul

variable {R' R'' A : Type*} [Monoid R'] [Monoid R''] [Semiring A] [Module A M] [Module A N]
  [DistribMulAction R' N] [ContinuousConstSMul R' N] [SMulCommClass A R' N] [DistribMulAction R'' N]
  [ContinuousConstSMul R'' N] [SMulCommClass A R'' N]

instance : SMul R' (M [Î›^Î¹]â†’L[A] N) :=
  âŸ¨fun c f => âŸ¨c â€¢ f.1, (c â€¢ f.toAlternatingMap).map_eq_zero_of_eqâŸ©âŸ©

@[simp]
theorem coe_smul (f : M [Î›^Î¹]â†’L[A] N) (c : R') : â‡‘(c â€¢ f) = c â€¢ â‡‘f :=
  rfl

theorem smul_apply (f : M [Î›^Î¹]â†’L[A] N) (c : R') (v : Î¹ â†’ M) : (c â€¢ f) v = c â€¢ f v :=
  rfl

@[simp]
theorem toContinuousMultilinearMap_smul (c : R') (f : M [Î›^Î¹]â†’L[A] N) :
    (c â€¢ f).toContinuousMultilinearMap = c â€¢ f.toContinuousMultilinearMap :=
  rfl

@[simp]
theorem toAlternatingMap_smul (c : R') (f : M [Î›^Î¹]â†’L[A] N) :
    (c â€¢ f).toAlternatingMap = c â€¢ f.toAlternatingMap :=
  rfl

instance [SMulCommClass R' R'' N] : SMulCommClass R' R'' (M [Î›^Î¹]â†’L[A] N) :=
  âŸ¨fun _ _ _ => ext fun _ => smul_comm _ _ _âŸ©

instance [SMul R' R''] [IsScalarTower R' R'' N] : IsScalarTower R' R'' (M [Î›^Î¹]â†’L[A] N) :=
  âŸ¨fun _ _ _ => ext fun _ => smul_assoc _ _ _âŸ©

instance [DistribMulAction R'áµáµ’áµ– N] [IsCentralScalar R' N] : IsCentralScalar R' (M [Î›^Î¹]â†’L[A] N) :=
  âŸ¨fun _ _ => ext fun _ => op_smul_eq_smul _ _âŸ©

instance : MulAction R' (M [Î›^Î¹]â†’L[A] N) :=
  toContinuousMultilinearMap_injective.mulAction toContinuousMultilinearMap fun _ _ => rfl

end SMul

section ContinuousAdd

variable [ContinuousAdd N]

instance : Add (M [Î›^Î¹]â†’L[R] N) :=
  âŸ¨fun f g => âŸ¨f.1 + g.1, (f.toAlternatingMap + g.toAlternatingMap).map_eq_zero_of_eqâŸ©âŸ©

@[simp]
theorem coe_add : â‡‘(f + g) = â‡‘f + â‡‘g :=
  rfl

@[simp]
theorem add_apply (v : Î¹ â†’ M) : (f + g) v = f v + g v :=
  rfl

@[simp]
theorem toContinuousMultilinearMap_add (f g : M [Î›^Î¹]â†’L[R] N) : (f + g).1 = f.1 + g.1 :=
  rfl

@[simp]
theorem toAlternatingMap_add (f g : M [Î›^Î¹]â†’L[R] N) :
    (f + g).toAlternatingMap = f.toAlternatingMap + g.toAlternatingMap :=
  rfl

instance addCommMonoid : AddCommMonoid (M [Î›^Î¹]â†’L[R] N) :=
  toContinuousMultilinearMap_injective.addCommMonoid _ rfl (fun _ _ => rfl) fun _ _ => rfl

/-- Evaluation of a `ContinuousAlternatingMap` at a vector as an `AddMonoidHom`. -/
def applyAddHom (v : Î¹ â†’ M) : M [Î›^Î¹]â†’L[R] N â†’+ N :=
  âŸ¨âŸ¨fun f => f v, rflâŸ©, fun _ _ => rflâŸ©

@[simp]
theorem sum_apply {Î± : Type*} (f : Î± â†’ M [Î›^Î¹]â†’L[R] N) (m : Î¹ â†’ M) {s : Finset Î±} :
    (âˆ‘ a in s, f a) m = âˆ‘ a in s, f a m :=
  (applyAddHom m).map_sum f s

/-- Projection to `ContinuousMultilinearMap`s as a bundled `AddMonoidHom`. -/
@[simps]
def toMultilinearAddHom : M [Î›^Î¹]â†’L[R] N â†’+ ContinuousMultilinearMap R (fun _ : Î¹ => M) N :=
  âŸ¨âŸ¨fun f => f.1, rflâŸ©, fun _ _ => rflâŸ©

end ContinuousAdd

/-- If `f` is a continuous alternating map, then `f.toContinuousLinearMap m i` is the continuous
linear map obtained by fixing all coordinates but `i` equal to those of `m`, and varying the
`i`-th coordinate. -/
@[simps! apply]
def toContinuousLinearMap [DecidableEq Î¹] (m : Î¹ â†’ M) (i : Î¹) : M â†’L[R] N :=
  f.1.toContinuousLinearMap m i

/-- The cartesian product of two continuous alternating maps, as a continuous alternating map. -/
@[simps!]
def prod (f : M [Î›^Î¹]â†’L[R] N) (g : M [Î›^Î¹]â†’L[R] N') : M [Î›^Î¹]â†’L[R] (N Ã— N') :=
  âŸ¨f.1.prod g.1, (f.toAlternatingMap.prod g.toAlternatingMap).map_eq_zero_of_eqâŸ©

/-- Combine a family of continuous alternating maps with the same domain and codomains `M' i` into a
continuous alternating map taking values in the space of functions `Î  i, M' i`. -/
def pi {Î¹' : Type*} {M' : Î¹' â†’ Type*} [âˆ€ i, AddCommMonoid (M' i)] [âˆ€ i, TopologicalSpace (M' i)]
    [âˆ€ i, Module R (M' i)] (f : âˆ€ i, M [Î›^Î¹]â†’L[R] M' i) : M [Î›^Î¹]â†’L[R] âˆ€ i, M' i :=
  âŸ¨ContinuousMultilinearMap.pi fun i => (f i).1,
    (AlternatingMap.pi fun i => (f i).toAlternatingMap).map_eq_zero_of_eqâŸ©

@[simp]
theorem coe_pi {Î¹' : Type*} {M' : Î¹' â†’ Type*} [âˆ€ i, AddCommMonoid (M' i)]
    [âˆ€ i, TopologicalSpace (M' i)] [âˆ€ i, Module R (M' i)] (f : âˆ€ i, M [Î›^Î¹]â†’L[R] M' i) :
    â‡‘(pi f) = fun m j => f j m :=
  rfl

theorem pi_apply {Î¹' : Type*} {M' : Î¹' â†’ Type*} [âˆ€ i, AddCommMonoid (M' i)]
    [âˆ€ i, TopologicalSpace (M' i)] [âˆ€ i, Module R (M' i)] (f : âˆ€ i, M [Î›^Î¹]â†’L[R] M' i) (m : Î¹ â†’ M)
    (j : Î¹') : pi f m j = f j m :=
  rfl

section

variable (R M)

/-- The evaluation map from `Î¹ â†’ M` to `M` is alternating at a given `i` when `Î¹` is subsingleton.
-/
@[simps! toContinuousMultilinearMap apply]
def ofSubsingleton [Subsingleton Î¹] (i' : Î¹) : M [Î›^Î¹]â†’L[R] M :=
  { AlternatingMap.ofSubsingleton R _ i' with
    toContinuousMultilinearMap := ContinuousMultilinearMap.ofSubsingleton R _ i' }

@[simp]
theorem ofSubsingleton_toAlternatingMap [Subsingleton Î¹] (i' : Î¹) :
    (ofSubsingleton R M i').toAlternatingMap = AlternatingMap.ofSubsingleton R M i' :=
  rfl

variable (Î¹)

/-- The constant map is alternating when `Î¹` is empty. -/
@[simps! toContinuousMultilinearMap apply]
def constOfIsEmpty [IsEmpty Î¹] (m : N) : M [Î›^Î¹]â†’L[R] N :=
  { AlternatingMap.constOfIsEmpty R M Î¹ m with
    toContinuousMultilinearMap := ContinuousMultilinearMap.constOfIsEmpty R (fun _ => M) m }

@[simp]
theorem constOfIsEmpty_toAlternatingMap [IsEmpty Î¹] (m : N) :
    (constOfIsEmpty R M Î¹ m).toAlternatingMap = AlternatingMap.constOfIsEmpty R M Î¹ m :=
  rfl

end

/-- If `g` is continuous alternating and `f` is a continuous linear map, then `g (f mâ‚, ..., f mâ‚™)`
is again a continuous alternating map, that we call `g.compContinuousLinearMap f`. -/
def compContinuousLinearMap (g : M [Î›^Î¹]â†’L[R] N) (f : M' â†’L[R] M) : M' [Î›^Î¹]â†’L[R] N :=
  { g.toAlternatingMap.compLinearMap (f : M' â†’â‚—[R] M) with
    toContinuousMultilinearMap := g.1.compContinuousLinearMap fun _ => f }

@[simp]
theorem compContinuousLinearMap_apply (g : M [Î›^Î¹]â†’L[R] N) (f : M' â†’L[R] M) (m : Î¹ â†’ M') :
    g.compContinuousLinearMap f m = g (f âˆ˜ m) :=
  rfl

/-- Composing a continuous alternating map with a continuous linear map gives again a
continuous alternating map. -/
def _root_.ContinuousLinearMap.compContinuousAlternatingMap (g : N â†’L[R] N') (f : M [Î›^Î¹]â†’L[R] N) :
    M [Î›^Î¹]â†’L[R] N' :=
  { (g : N â†’â‚—[R] N').compAlternatingMap f.toAlternatingMap with
    toContinuousMultilinearMap := g.compContinuousMultilinearMap f.1 }

@[simp]
theorem _root_.ContinuousLinearMap.compContinuousAlternatingMap_coe (g : N â†’L[R] N')
    (f : M [Î›^Î¹]â†’L[R] N) : â‡‘(g.compContinuousAlternatingMap f) = g âˆ˜ f :=
  rfl

/-- A continuous linear equivalence of domains defines an equivalence between continuous
alternating maps. -/
def _root_.ContinuousLinearEquiv.continuousAlternatingMapComp (e : M â‰ƒL[R] M') :
    M [Î›^Î¹]â†’L[R] N â‰ƒ M' [Î›^Î¹]â†’L[R] N where
  toFun f := f.compContinuousLinearMap â†‘e.symm
  invFun f := f.compContinuousLinearMap â†‘e
  left_inv f := by ext; simp [(Â· âˆ˜ Â·)]
                   -- âŠ¢ â†‘((fun f => compContinuousLinearMap f â†‘e) ((fun f => compContinuousLinearMap â€¦
                        -- ðŸŽ‰ no goals
  right_inv f := by ext; simp [(Â· âˆ˜ Â·)]
                    -- âŠ¢ â†‘((fun f => compContinuousLinearMap f â†‘(ContinuousLinearEquiv.symm e)) ((fun â€¦
                         -- ðŸŽ‰ no goals

/-- A continuous linear equivalence of codomains defines an equivalence between continuous
alternating maps. -/
def _root_.ContinuousLinearEquiv.compContinuousAlternatingMap (e : N â‰ƒL[R] N') :
    M [Î›^Î¹]â†’L[R] N â‰ƒ M [Î›^Î¹]â†’L[R] N' where
  toFun := (e : N â†’L[R] N').compContinuousAlternatingMap
  invFun := (e.symm : N' â†’L[R] N).compContinuousAlternatingMap
  left_inv f := by ext; simp [(Â· âˆ˜ Â·)]
                   -- âŠ¢ â†‘(ContinuousLinearMap.compContinuousAlternatingMap (â†‘(ContinuousLinearEquiv. â€¦
                        -- ðŸŽ‰ no goals
  right_inv f := by ext; simp [(Â· âˆ˜ Â·)]
                    -- âŠ¢ â†‘(ContinuousLinearMap.compContinuousAlternatingMap (â†‘e) (ContinuousLinearMap â€¦
                         -- ðŸŽ‰ no goals

@[simp]
theorem _root_.ContinuousLinearEquiv.compContinuousAlternatingMap_coe
    (e : N â‰ƒL[R] N') (f : M [Î›^Î¹]â†’L[R] N) : â‡‘(e.compContinuousAlternatingMap f) = e âˆ˜ f :=
  rfl

/-- Continuous linear equivalences between domains and codomains define an equivalence between the
spaces of continuous alternating maps. -/
def _root_.ContinuousLinearEquiv.continuousAlternatingMapCongr (e : M â‰ƒL[R] M') (e' : N â‰ƒL[R] N') :
    M [Î›^Î¹]â†’L[R] N â‰ƒ M' [Î›^Î¹]â†’L[R] N' :=
  e.continuousAlternatingMapComp.trans e'.compContinuousAlternatingMap

/-- `ContinuousAlternatingMap.pi` as an `Equiv`. -/
@[simps]
def piEquiv {Î¹' : Type*} {N : Î¹' â†’ Type*} [âˆ€ i, AddCommMonoid (N i)] [âˆ€ i, TopologicalSpace (N i)]
    [âˆ€ i, Module R (N i)] : (âˆ€ i, M [Î›^Î¹]â†’L[R] N i) â‰ƒ M [Î›^Î¹]â†’L[R] âˆ€ i, N i where
  toFun := pi
  invFun f i := (ContinuousLinearMap.proj i : _ â†’L[R] N i).compContinuousAlternatingMap f
  left_inv f := by ext; rfl
                   -- âŠ¢ â†‘((fun f i => ContinuousLinearMap.compContinuousAlternatingMap (ContinuousLi â€¦
                        -- ðŸŽ‰ no goals
  right_inv f := by ext; rfl
                    -- âŠ¢ â†‘(pi ((fun f i => ContinuousLinearMap.compContinuousAlternatingMap (Continuo â€¦
                         -- ðŸŽ‰ no goals

/-- In the specific case of continuous alternating maps on spaces indexed by `Fin (n+1)`, where one
can build an element of `Î (i : Fin (n+1)), M i` using `cons`, one can express directly the
additivity of an alternating map along the first variable. -/
theorem cons_add (f : ContinuousAlternatingMap R M N (Fin (n + 1))) (m : Fin n â†’ M) (x y : M) :
    f (Fin.cons (x + y) m) = f (Fin.cons x m) + f (Fin.cons y m) :=
  f.toMultilinearMap.cons_add m x y

/-- In the specific case of continuous alternating maps on spaces indexed by `Fin (n+1)`, where one
can build an element of `Î (i : Fin (n+1)), M i` using `cons`, one can express directly the
additivity of an alternating map along the first variable. -/
theorem vecCons_add (f : ContinuousAlternatingMap R M N (Fin (n + 1))) (m : Fin n â†’ M) (x y : M) :
    f (vecCons (x + y) m) = f (vecCons x m) + f (vecCons y m) :=
  f.toMultilinearMap.cons_add m x y

/-- In the specific case of continuous alternating maps on spaces indexed by `Fin (n+1)`, where one
can build an element of `Î (i : Fin (n+1)), M i` using `cons`, one can express directly the
multiplicativity of an alternating map along the first variable. -/
theorem cons_smul (f : ContinuousAlternatingMap R M N (Fin (n + 1))) (m : Fin n â†’ M) (c : R)
    (x : M) : f (Fin.cons (c â€¢ x) m) = c â€¢ f (Fin.cons x m) :=
  f.toMultilinearMap.cons_smul m c x

/-- In the specific case of continuous alternating maps on spaces indexed by `Fin (n+1)`, where one
can build an element of `Î (i : Fin (n+1)), M i` using `cons`, one can express directly the
multiplicativity of an alternating map along the first variable. -/
theorem vecCons_smul (f : ContinuousAlternatingMap R M N (Fin (n + 1))) (m : Fin n â†’ M) (c : R)
    (x : M) : f (vecCons (c â€¢ x) m) = c â€¢ f (vecCons x m) :=
  f.toMultilinearMap.cons_smul m c x

theorem map_piecewise_add [DecidableEq Î¹] (m m' : Î¹ â†’ M) (t : Finset Î¹) :
    f (t.piecewise (m + m') m') = âˆ‘ s in t.powerset, f (s.piecewise m m') :=
  f.toMultilinearMap.map_piecewise_add _ _ _

/-- Additivity of a continuous alternating map along all coordinates at the same time,
writing `f (m + m')` as the sum of `f (s.piecewise m m')` over all sets `s`. -/
theorem map_add_univ [DecidableEq Î¹] [Fintype Î¹] (m m' : Î¹ â†’ M) :
    f (m + m') = âˆ‘ s : Finset Î¹, f (s.piecewise m m') :=
  f.toMultilinearMap.map_add_univ _ _

section ApplySum

open Fintype Finset

variable {Î± : Î¹ â†’ Type*} [Fintype Î¹] [DecidableEq Î¹] (g' : âˆ€ i, Î± i â†’ M) (A : âˆ€ i, Finset (Î± i))

/-- If `f` is continuous alternating, then `f (Î£_{jâ‚ âˆˆ Aâ‚} gâ‚ jâ‚, ..., Î£_{jâ‚™ âˆˆ Aâ‚™} gâ‚™ jâ‚™)` is the
sum of `f (gâ‚ (r 1), ..., gâ‚™ (r n))` where `r` ranges over all functions with `r 1 âˆˆ Aâ‚`, ...,
`r n âˆˆ Aâ‚™`. This follows from multilinearity by expanding successively with respect to each
coordinate. -/
theorem map_sum_finset :
    (f fun i => âˆ‘ j in A i, g' i j) = âˆ‘ r in piFinset A, f fun i => g' i (r i) :=
  f.toMultilinearMap.map_sum_finset _ _

/-- If `f` is continuous alternating, then `f (Î£_{jâ‚} gâ‚ jâ‚, ..., Î£_{jâ‚™} gâ‚™ jâ‚™)` is the sum of
`f (gâ‚ (r 1), ..., gâ‚™ (r n))` where `r` ranges over all functions `r`. This follows from
multilinearity by expanding successively with respect to each coordinate. -/
theorem map_sum [âˆ€ i, Fintype (Î± i)] :
    (f fun i => âˆ‘ j, g' i j) = âˆ‘ r : âˆ€ i, Î± i, f fun i => g' i (r i) :=
  f.toMultilinearMap.map_sum _

end ApplySum

section RestrictScalar

variable (R)
variable {A : Type*} [Semiring A] [SMul R A] [Module A M] [Module A N] [IsScalarTower R A M]
  [IsScalarTower R A N]

/-- Reinterpret a continuous `A`-alternating map as a continuous `R`-alternating map, if `A` is an
algebra over `R` and their actions on all involved modules agree with the action of `R` on `A`. -/
def restrictScalars (f : M [Î›^Î¹]â†’L[A] N) : M [Î›^Î¹]â†’L[R] N :=
  { f with toContinuousMultilinearMap := f.1.restrictScalars R }

@[simp]
theorem coe_restrictScalars (f : M [Î›^Î¹]â†’L[A] N) : â‡‘(f.restrictScalars R) = f :=
  rfl

end RestrictScalar

end Semiring

section Ring

variable {R M M' N N' Î¹ : Type*} [Ring R] [AddCommGroup M] [Module R M] [TopologicalSpace M]
  [AddCommGroup M'] [Module R M'] [TopologicalSpace M'] [AddCommGroup N] [Module R N]
  [TopologicalSpace N] [AddCommGroup N'] [Module R N'] [TopologicalSpace N'] {n : â„•}
  (f g : M [Î›^Î¹]â†’L[R] N)

@[simp]
theorem map_sub [DecidableEq Î¹] (m : Î¹ â†’ M) (i : Î¹) (x y : M) :
    f (update m i (x - y)) = f (update m i x) - f (update m i y) :=
  f.toMultilinearMap.map_sub _ _ _ _

section TopologicalAddGroup

variable [TopologicalAddGroup N]

instance : Neg (M [Î›^Î¹]â†’L[R] N) :=
  âŸ¨fun f => { -f.toAlternatingMap with toContinuousMultilinearMap := -f.1 }âŸ©

@[simp]
theorem coe_neg : â‡‘(-f) = -f :=
  rfl

theorem neg_apply (m : Î¹ â†’ M) : (-f) m = -f m :=
  rfl

instance : Sub (M [Î›^Î¹]â†’L[R] N) :=
  âŸ¨fun f g =>
    { f.toAlternatingMap - g.toAlternatingMap with toContinuousMultilinearMap := f.1 - g.1 }âŸ©

@[simp] theorem coe_sub : â‡‘(f - g) = â‡‘f - â‡‘g := rfl

theorem sub_apply (m : Î¹ â†’ M) : (f - g) m = f m - g m := rfl

instance : AddCommGroup (M [Î›^Î¹]â†’L[R] N) :=
  toContinuousMultilinearMap_injective.addCommGroup _ rfl (fun _ _ => rfl) (fun _ => rfl)
    (fun _ _ => rfl) (fun _ _ => rfl) fun _ _ => rfl

end TopologicalAddGroup

end Ring

section CommSemiring

variable {R M M' N N' Î¹ : Type*} [CommSemiring R] [AddCommMonoid M] [Module R M]
  [TopologicalSpace M] [AddCommMonoid M'] [Module R M'] [TopologicalSpace M'] [AddCommMonoid N]
  [Module R N] [TopologicalSpace N] [AddCommMonoid N'] [Module R N'] [TopologicalSpace N'] {n : â„•}
  (f g : M [Î›^Î¹]â†’L[R] N)

theorem map_piecewise_smul [DecidableEq Î¹] (c : Î¹ â†’ R) (m : Î¹ â†’ M) (s : Finset Î¹) :
    f (s.piecewise (fun i => c i â€¢ m i) m) = (âˆ i in s, c i) â€¢ f m :=
  f.toMultilinearMap.map_piecewise_smul _ _ _

/-- Multiplicativity of a continuous alternating map along all coordinates at the same time,
writing `f (Î» i, c i â€¢ m i)` as `(âˆ i, c i) â€¢ f m`. -/
theorem map_smul_univ [Fintype Î¹] (c : Î¹ â†’ R) (m : Î¹ â†’ M) :
    (f fun i => c i â€¢ m i) = (âˆ i, c i) â€¢ f m :=
  f.toMultilinearMap.map_smul_univ _ _

end CommSemiring

section DistribMulAction

variable {R A M N Î¹ : Type*} [Monoid R] [Semiring A] [AddCommMonoid M] [AddCommMonoid N]
  [TopologicalSpace M] [TopologicalSpace N] [Module A M] [Module A N] [DistribMulAction R N]
  [ContinuousConstSMul R N] [SMulCommClass A R N]

instance [ContinuousAdd N] : DistribMulAction R (M [Î›^Î¹]â†’L[A] N) :=
  Function.Injective.distribMulAction toMultilinearAddHom
    toContinuousMultilinearMap_injective fun _ _ => rfl

end DistribMulAction

section Module

variable {R A M N Î¹ : Type*} [Semiring R] [Semiring A] [AddCommMonoid M] [AddCommMonoid N]
  [TopologicalSpace M] [TopologicalSpace N] [ContinuousAdd N] [Module A M] [Module A N] [Module R N]
  [ContinuousConstSMul R N] [SMulCommClass A R N]

/-- The space of continuous alternating maps over an algebra over `R` is a module over `R`, for the
pointwise addition and scalar multiplication. -/
instance : Module R (M [Î›^Î¹]â†’L[A] N) :=
  Function.Injective.module _ toMultilinearAddHom toContinuousMultilinearMap_injective fun _ _ =>
    rfl

/-- Linear map version of the map `toMultilinearMap` associating to a continuous alternating map
the corresponding multilinear map. -/
@[simps]
def toContinuousMultilinearMapLinear :
    M [Î›^Î¹]â†’L[A] N â†’â‚—[R] ContinuousMultilinearMap A (fun _ : Î¹ => M) N where
  toFun := toContinuousMultilinearMap
  map_add' _ _ := rfl
  map_smul' _ _ := rfl

/-- `ContinuousAlternatingMap.pi` as a `LinearEquiv`. -/
@[simps (config := { simpRhs := true })]
def piLinearEquiv {Î¹' : Type*} {M' : Î¹' â†’ Type*} [âˆ€ i, AddCommMonoid (M' i)]
    [âˆ€ i, TopologicalSpace (M' i)] [âˆ€ i, ContinuousAdd (M' i)] [âˆ€ i, Module R (M' i)]
    [âˆ€ i, Module A (M' i)] [âˆ€ i, SMulCommClass A R (M' i)] [âˆ€ i, ContinuousConstSMul R (M' i)] :
    (âˆ€ i, M [Î›^Î¹]â†’L[A] M' i) â‰ƒâ‚—[R] M [Î›^Î¹]â†’L[A] âˆ€ i, M' i :=
  { piEquiv with
    map_add' := fun _ _ => rfl
    map_smul' := fun _ _ => rfl }

end Module

section SmulRight

variable {R A M N Î¹ : Type*} [CommSemiring R] [AddCommMonoid M] [AddCommMonoid N] [Module R M]
  [Module R N] [TopologicalSpace R] [TopologicalSpace M] [TopologicalSpace N] [ContinuousSMul R N]
  (f : M [Î›^Î¹]â†’L[R] R) (z : N)

/-- Given a continuous `R`-alternating map `f` taking values in `R`, `f.smulRight z` is the
continuous alternating map sending `m` to `f m â€¢ z`. -/
@[simps! toContinuousMultilinearMap apply]
def smulRight : M [Î›^Î¹]â†’L[R] N :=
  { f.toAlternatingMap.smulRight z with toContinuousMultilinearMap := f.1.smulRight z }

end SmulRight

section Semiring

variable {R M M' N N' Î¹ : Type*} [CommSemiring R] [AddCommMonoid M] [Module R M]
  [TopologicalSpace M] [AddCommMonoid M'] [Module R M'] [TopologicalSpace M'] [AddCommMonoid N]
  [Module R N] [TopologicalSpace N] [ContinuousAdd N] [ContinuousConstSMul R N] [AddCommMonoid N']
  [Module R N'] [TopologicalSpace N'] [ContinuousAdd N'] [ContinuousConstSMul R N']

/-- `ContinuousAlternatingMap.compContinuousLinearMap` as a bundled `LinearMap`. -/
@[simps]
def compContinuousLinearMapâ‚— (f : M â†’L[R] M') : (M' [Î›^Î¹]â†’L[R] N) â†’â‚—[R] (M [Î›^Î¹]â†’L[R] N) where
  toFun g := g.compContinuousLinearMap f
  map_add' g g' := by ext; simp
                      -- âŠ¢ â†‘((fun g => compContinuousLinearMap g f) (g + g')) xâœ = â†‘((fun g => compCont â€¦
                           -- ðŸŽ‰ no goals
  map_smul' c g := by ext; simp
                      -- âŠ¢ â†‘(AddHom.toFun { toFun := fun g => compContinuousLinearMap g f, map_add' :=  â€¦
                           -- ðŸŽ‰ no goals

variable (R M N N')

/-- `ContinuousLinearMap.compContinuousAlternatingMap` as a bundled bilinear map. -/
def _root_.ContinuousLinearMap.compContinuousAlternatingMapâ‚— :
    (N â†’L[R] N') â†’â‚—[R] (M [Î›^Î¹]â†’L[R] N) â†’â‚—[R] (M [Î›^Î¹]â†’L[R] N') :=
  LinearMap.mkâ‚‚ R ContinuousLinearMap.compContinuousAlternatingMap (fun fâ‚ fâ‚‚ g => rfl)
    (fun c f g => rfl) (fun f gâ‚ gâ‚‚ => by ext1; apply f.map_add) fun c f g => by ext1; simp
                                          -- âŠ¢ â†‘(ContinuousLinearMap.compContinuousAlternatingMap f (gâ‚ + gâ‚‚)) xâœ = â†‘(Conti â€¦
                                                -- ðŸŽ‰ no goals
                                                                                 -- âŠ¢ â†‘(ContinuousLinearMap.compContinuousAlternatingMap f (c â€¢ g)) xâœ = â†‘(c â€¢ Con â€¦
                                                                                       -- ðŸŽ‰ no goals

end Semiring

end ContinuousAlternatingMap

namespace ContinuousMultilinearMap

variable {R M N Î¹ : Type*} [Semiring R] [AddCommMonoid M] [Module R M] [TopologicalSpace M]
  [AddCommGroup N] [Module R N] [TopologicalSpace N] [TopologicalAddGroup N] [Fintype Î¹]
  [DecidableEq Î¹] (f g : ContinuousMultilinearMap R (fun _ : Î¹ => M) N)

/-- Alternatization of a continuous multilinear map. -/
@[simps (config := { isSimp := false }) apply_toContinuousMultilinearMap]
def alternatization : ContinuousMultilinearMap R (fun _ : Î¹ => M) N â†’+ M [Î›^Î¹]â†’L[R] N where
  toFun f :=
    { toContinuousMultilinearMap := âˆ‘ Ïƒ : Equiv.Perm Î¹, Equiv.Perm.sign Ïƒ â€¢ f.domDomCongr Ïƒ
      map_eq_zero_of_eq' := fun v i j hv hne => by
        simpa [MultilinearMap.alternatization_apply]
          using f.1.alternatization.map_eq_zero_of_eq' v i j hv hne }
  map_zero' := by ext; simp
                  -- âŠ¢ â†‘((fun f => { toContinuousMultilinearMap := âˆ‘ Ïƒ : Equiv.Perm Î¹, â†‘Equiv.Perm. â€¦
                       -- ðŸŽ‰ no goals
  map_add' _ _ := by ext; simp [Finset.sum_add_distrib]
                     -- âŠ¢ â†‘(ZeroHom.toFun { toFun := fun f => { toContinuousMultilinearMap := âˆ‘ Ïƒ : Eq â€¦
                          -- ðŸŽ‰ no goals

theorem alternatization_apply_apply (v : Î¹ â†’ M) :
    alternatization f v = âˆ‘ Ïƒ : Equiv.Perm Î¹, Equiv.Perm.sign Ïƒ â€¢ f (v âˆ˜ Ïƒ) := by
  simp [alternatization, (Â· âˆ˜ Â·)]
  -- ðŸŽ‰ no goals

@[simp]
theorem alternatization_apply_toAlternatingMap :
    (alternatization f).toAlternatingMap = MultilinearMap.alternatization f.1 := by
  ext v
  -- âŠ¢ â†‘(ContinuousAlternatingMap.toAlternatingMap (â†‘alternatization f)) v = â†‘(â†‘Mul â€¦
  simp [alternatization_apply_apply, MultilinearMap.alternatization_apply, (Â· âˆ˜ Â·)]
  -- ðŸŽ‰ no goals

end ContinuousMultilinearMap
