/-
Copyright (c) 2021 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤, Moritz Doll
-/
import Mathlib.Topology.Algebra.Module.Basic
import Mathlib.LinearAlgebra.BilinearMap

#align_import topology.algebra.module.weak_dual from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Weak dual topology

This file defines the weak topology given two vector spaces `E` and `F` over a commutative semiring
`ğ•œ` and a bilinear form `B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ`. The weak topology on `E` is the coarsest topology
such that for all `y : F` every map `fun x => B x y` is continuous.

In the case that `F = E â†’L[ğ•œ] ğ•œ` and `B` being the canonical pairing, we obtain the weak-* topology,
`WeakDual ğ•œ E := (E â†’L[ğ•œ] ğ•œ)`. Interchanging the arguments in the bilinear form yields the
weak topology `WeakSpace ğ•œ E := E`.

## Main definitions

The main definitions are the types `WeakBilin B` for the general case and the two special cases
`WeakDual ğ•œ E` and `WeakSpace ğ•œ E` with the respective topology instances on it.

* Given `B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ`, the type `WeakBilin B` is a type synonym for `E`.
* The instance `WeakBilin.instTopologicalSpace` is the weak topology induced by the bilinear form
  `B`.
* `WeakDual ğ•œ E` is a type synonym for `Dual ğ•œ E` (when the latter is defined): both are equal to
  the type `E â†’L[ğ•œ] ğ•œ` of continuous linear maps from a module `E` over `ğ•œ` to the ring `ğ•œ`.
* The instance `WeakDual.instTopologicalSpace` is the weak-* topology on `WeakDual ğ•œ E`, i.e., the
  coarsest topology making the evaluation maps at all `z : E` continuous.
* `WeakSpace ğ•œ E` is a type synonym for `E` (when the latter is defined).
* The instance `WeakSpace.instTopologicalSpace` is the weak topology on `E`, i.e., the
  coarsest topology such that all `v : dual ğ•œ E` remain continuous.

## Main results

We establish that `WeakBilin B` has the following structure:
* `WeakBilin.instContinuousAdd`: The addition in `WeakBilin B` is continuous.
* `WeakBilin.instContinuousSMul`: The scalar multiplication in `WeakBilin B` is continuous.

We prove the following results characterizing the weak topology:
* `eval_continuous`: For any `y : F`, the evaluation mapping `fun x => B x y` is continuous.
* `continuous_of_continuous_eval`: For a mapping to `WeakBilin B` to be continuous,
  it suffices that its compositions with pairing with `B` at all points `y : F` is continuous.
* `tendsto_iff_forall_eval_tendsto`: Convergence in `WeakBilin B` can be characterized
  in terms of convergence of the evaluations at all points `y : F`.

## Notations

No new notation is introduced.

## References

* [H. H. Schaefer, *Topological Vector Spaces*][schaefer1966]

## Tags

weak-star, weak dual, duality

-/


noncomputable section

open Filter

open Topology

variable {Î± ğ•œ ğ• R E F M : Type*}

section WeakTopology

/-- The space `E` equipped with the weak topology induced by the bilinear form `B`. -/
@[nolint unusedArguments]
def WeakBilin [CommSemiring ğ•œ] [AddCommMonoid E] [Module ğ•œ E] [AddCommMonoid F] [Module ğ•œ F]
    (_ : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) := E
#align weak_bilin WeakBilin

namespace WeakBilin

-- Porting note: the next two instances should be derived from the definition
instance instAddCommMonoid [CommSemiring ğ•œ] [a : AddCommMonoid E] [Module ğ•œ E] [AddCommMonoid F]
    [Module ğ•œ F] (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) : AddCommMonoid (WeakBilin B) := a

instance instModule [CommSemiring ğ•œ] [AddCommMonoid E] [m : Module ğ•œ E] [AddCommMonoid F]
    [Module ğ•œ F] (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) : Module ğ•œ (WeakBilin B) := m

instance instAddCommGroup [CommSemiring ğ•œ] [a : AddCommGroup E] [Module ğ•œ E] [AddCommMonoid F]
    [Module ğ•œ F] (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) : AddCommGroup (WeakBilin B) := a

instance (priority := 100) instModule' [CommSemiring ğ•œ] [CommSemiring ğ•] [AddCommGroup E]
    [Module ğ•œ E] [AddCommGroup F] [Module ğ•œ F] [m : Module ğ• E] (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) :
    Module ğ• (WeakBilin B) := m
#align weak_bilin.module' WeakBilin.instModule'

instance instIsScalarTower [CommSemiring ğ•œ] [CommSemiring ğ•] [AddCommGroup E] [Module ğ•œ E]
    [AddCommGroup F] [Module ğ•œ F] [SMul ğ• ğ•œ] [Module ğ• E] [s : IsScalarTower ğ• ğ•œ E]
    (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) : IsScalarTower ğ• ğ•œ (WeakBilin B) := s

section Semiring

variable [TopologicalSpace ğ•œ] [CommSemiring ğ•œ]
variable [AddCommMonoid E] [Module ğ•œ E]
variable [AddCommMonoid F] [Module ğ•œ F]
variable (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ)

instance instTopologicalSpace : TopologicalSpace (WeakBilin B) :=
  TopologicalSpace.induced (fun x y => B x y) Pi.topologicalSpace

/-- The coercion `(fun x y => B x y) : E â†’ (F â†’ ğ•œ)` is continuous. -/
theorem coeFn_continuous : Continuous fun (x : WeakBilin B) y => B x y :=
  continuous_induced_dom
#align weak_bilin.coe_fn_continuous WeakBilin.coeFn_continuous

theorem eval_continuous (y : F) : Continuous fun x : WeakBilin B => B x y :=
  (continuous_pi_iff.mp (coeFn_continuous B)) y
#align weak_bilin.eval_continuous WeakBilin.eval_continuous

theorem continuous_of_continuous_eval [TopologicalSpace Î±] {g : Î± â†’ WeakBilin B}
    (h : âˆ€ y, Continuous fun a => B (g a) y) : Continuous g :=
  continuous_induced_rng.2 (continuous_pi_iff.mpr h)
#align weak_bilin.continuous_of_continuous_eval WeakBilin.continuous_of_continuous_eval

/-- The coercion `(fun x y => B x y) : E â†’ (F â†’ ğ•œ)` is an embedding. -/
theorem embedding {B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ} (hB : Function.Injective B) :
    Embedding fun (x : WeakBilin B) y => B x y :=
  Function.Injective.embedding_induced <| LinearMap.coe_injective.comp hB
#align weak_bilin.embedding WeakBilin.embedding

theorem tendsto_iff_forall_eval_tendsto {l : Filter Î±} {f : Î± â†’ WeakBilin B} {x : WeakBilin B}
    (hB : Function.Injective B) :
    Tendsto f l (ğ“ x) â†” âˆ€ y, Tendsto (fun i => B (f i) y) l (ğ“ (B x y)) := by
  rw [â† tendsto_pi_nhds, Embedding.tendsto_nhds_iff (embedding hB)]
  rfl
#align weak_bilin.tendsto_iff_forall_eval_tendsto WeakBilin.tendsto_iff_forall_eval_tendsto

/-- Addition in `WeakBilin B` is continuous. -/
instance instContinuousAdd [ContinuousAdd ğ•œ] : ContinuousAdd (WeakBilin B) := by
  refine âŸ¨continuous_induced_rng.2 ?_âŸ©
  refine
    cast (congr_arg _ ?_)
      (((coeFn_continuous B).comp continuous_fst).add ((coeFn_continuous B).comp continuous_snd))
  ext
  simp only [Function.comp_apply, Pi.add_apply, map_add, LinearMap.add_apply]

/-- Scalar multiplication by `ğ•œ` on `WeakBilin B` is continuous. -/
instance instContinuousSMul [ContinuousSMul ğ•œ ğ•œ] : ContinuousSMul ğ•œ (WeakBilin B) := by
  refine âŸ¨continuous_induced_rng.2 ?_âŸ©
  refine cast (congr_arg _ ?_) (continuous_fst.smul ((coeFn_continuous B).comp continuous_snd))
  ext
  simp only [Function.comp_apply, Pi.smul_apply, LinearMap.map_smulâ‚›â‚—, RingHom.id_apply,
    LinearMap.smul_apply]

end Semiring

section Ring

variable [TopologicalSpace ğ•œ] [CommRing ğ•œ]
variable [AddCommGroup E] [Module ğ•œ E]
variable [AddCommGroup F] [Module ğ•œ F]


variable (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ)

/-- `WeakBilin B` is a `TopologicalAddGroup`, meaning that addition and negation are
continuous. -/
instance instTopologicalAddGroup [ContinuousAdd ğ•œ] : TopologicalAddGroup (WeakBilin B) where
  toContinuousAdd := by infer_instance
  continuous_neg := by
    refine continuous_induced_rng.2 (continuous_pi_iff.mpr fun y => ?_)
    refine cast (congr_arg _ ?_) (eval_continuous B (-y))
    ext x
    simp only [map_neg, Function.comp_apply, LinearMap.neg_apply]

end Ring

end WeakBilin

end WeakTopology

section WeakStarTopology

/-- The canonical pairing of a vector space and its topological dual. -/
def topDualPairing (ğ•œ E) [CommSemiring ğ•œ] [TopologicalSpace ğ•œ] [ContinuousAdd ğ•œ] [AddCommMonoid E]
    [Module ğ•œ E] [TopologicalSpace E] [ContinuousConstSMul ğ•œ ğ•œ] : (E â†’L[ğ•œ] ğ•œ) â†’â‚—[ğ•œ] E â†’â‚—[ğ•œ] ğ•œ :=
  ContinuousLinearMap.coeLM ğ•œ
#align top_dual_pairing topDualPairing

variable [CommSemiring ğ•œ] [TopologicalSpace ğ•œ] [ContinuousAdd ğ•œ]
variable [ContinuousConstSMul ğ•œ ğ•œ]
variable [AddCommMonoid E] [Module ğ•œ E] [TopologicalSpace E]

theorem topDualPairing_apply (v : E â†’L[ğ•œ] ğ•œ) (x : E) : topDualPairing ğ•œ E v x = v x :=
  rfl
#align dual_pairing_apply topDualPairing_apply

/-- The weak star topology is the topology coarsest topology on `E â†’L[ğ•œ] ğ•œ` such that all
functionals `fun v => v x` are continuous. -/
def WeakDual (ğ•œ E : Type*) [CommSemiring ğ•œ] [TopologicalSpace ğ•œ] [ContinuousAdd ğ•œ]
    [ContinuousConstSMul ğ•œ ğ•œ] [AddCommMonoid E] [Module ğ•œ E] [TopologicalSpace E] :=
  WeakBilin (topDualPairing ğ•œ E)
#align weak_dual WeakDual

namespace WeakDual

-- Porting note: the next four instances should be derived from the definition
instance instAddCommMonoid : AddCommMonoid (WeakDual ğ•œ E) :=
  WeakBilin.instAddCommMonoid (topDualPairing ğ•œ E)

instance instModule : Module ğ•œ (WeakDual ğ•œ E) :=
  WeakBilin.instModule (topDualPairing ğ•œ E)

instance instTopologicalSpace : TopologicalSpace (WeakDual ğ•œ E) :=
  WeakBilin.instTopologicalSpace (topDualPairing ğ•œ E)

instance instContinuousAdd : ContinuousAdd (WeakDual ğ•œ E) :=
  WeakBilin.instContinuousAdd (topDualPairing ğ•œ E)

instance instInhabited : Inhabited (WeakDual ğ•œ E) :=
  ContinuousLinearMap.inhabited

instance instFunLike : FunLike (WeakDual ğ•œ E) E ğ•œ :=
  ContinuousLinearMap.funLike

instance instContinuousLinearMapClass : ContinuousLinearMapClass (WeakDual ğ•œ E) ğ•œ E ğ•œ :=
  ContinuousLinearMap.continuousSemilinearMapClass
#align weak_dual.weak_dual.continuous_linear_map_class WeakDual.instContinuousLinearMapClass

/-- Helper instance for when there's too many metavariables to apply `DFunLike.hasCoeToFun`
directly. -/
instance : CoeFun (WeakDual ğ•œ E) fun _ => E â†’ ğ•œ :=
  DFunLike.hasCoeToFun

/-- If a monoid `M` distributively continuously acts on `ğ•œ` and this action commutes with
multiplication on `ğ•œ`, then it acts on `WeakDual ğ•œ E`. -/
instance instMulAction (M) [Monoid M] [DistribMulAction M ğ•œ] [SMulCommClass ğ•œ M ğ•œ]
    [ContinuousConstSMul M ğ•œ] : MulAction M (WeakDual ğ•œ E) :=
  ContinuousLinearMap.mulAction

/-- If a monoid `M` distributively continuously acts on `ğ•œ` and this action commutes with
multiplication on `ğ•œ`, then it acts distributively on `WeakDual ğ•œ E`. -/
instance instDistribMulAction (M) [Monoid M] [DistribMulAction M ğ•œ] [SMulCommClass ğ•œ M ğ•œ]
    [ContinuousConstSMul M ğ•œ] : DistribMulAction M (WeakDual ğ•œ E) :=
  ContinuousLinearMap.distribMulAction

/-- If `ğ•œ` is a topological module over a semiring `R` and scalar multiplication commutes with the
multiplication on `ğ•œ`, then `WeakDual ğ•œ E` is a module over `R`. -/
instance instModule' (R) [Semiring R] [Module R ğ•œ] [SMulCommClass ğ•œ R ğ•œ] [ContinuousConstSMul R ğ•œ] :
    Module R (WeakDual ğ•œ E) :=
  ContinuousLinearMap.module
#align weak_dual.module' WeakDual.instModule'

instance instContinuousConstSMul (M) [Monoid M] [DistribMulAction M ğ•œ] [SMulCommClass ğ•œ M ğ•œ]
    [ContinuousConstSMul M ğ•œ] : ContinuousConstSMul M (WeakDual ğ•œ E) :=
  âŸ¨fun m =>
    continuous_induced_rng.2 <| (WeakBilin.coeFn_continuous (topDualPairing ğ•œ E)).const_smul mâŸ©

/-- If a monoid `M` distributively continuously acts on `ğ•œ` and this action commutes with
multiplication on `ğ•œ`, then it continuously acts on `WeakDual ğ•œ E`. -/
instance instContinuousSMul (M) [Monoid M] [DistribMulAction M ğ•œ] [SMulCommClass ğ•œ M ğ•œ]
    [TopologicalSpace M] [ContinuousSMul M ğ•œ] : ContinuousSMul M (WeakDual ğ•œ E) :=
  âŸ¨continuous_induced_rng.2 <|
      continuous_fst.smul ((WeakBilin.coeFn_continuous (topDualPairing ğ•œ E)).comp continuous_snd)âŸ©

theorem coeFn_continuous : Continuous fun (x : WeakDual ğ•œ E) y => x y :=
  continuous_induced_dom
#align weak_dual.coe_fn_continuous WeakDual.coeFn_continuous

theorem eval_continuous (y : E) : Continuous fun x : WeakDual ğ•œ E => x y :=
  continuous_pi_iff.mp coeFn_continuous y
#align weak_dual.eval_continuous WeakDual.eval_continuous

theorem continuous_of_continuous_eval [TopologicalSpace Î±] {g : Î± â†’ WeakDual ğ•œ E}
    (h : âˆ€ y, Continuous fun a => (g a) y) : Continuous g :=
  continuous_induced_rng.2 (continuous_pi_iff.mpr h)
#align weak_dual.continuous_of_continuous_eval WeakDual.continuous_of_continuous_eval

instance instT2Space [T2Space ğ•œ] : T2Space (WeakDual ğ•œ E) :=
  Embedding.t2Space <|
    WeakBilin.embedding <|
      show Function.Injective (topDualPairing ğ•œ E) from ContinuousLinearMap.coe_injective

end WeakDual

/-- The weak topology is the topology coarsest topology on `E` such that all functionals
`fun x => v x` are continuous. -/
def WeakSpace (ğ•œ E) [CommSemiring ğ•œ] [TopologicalSpace ğ•œ] [ContinuousAdd ğ•œ]
    [ContinuousConstSMul ğ•œ ğ•œ] [AddCommMonoid E] [Module ğ•œ E] [TopologicalSpace E] :=
  WeakBilin (topDualPairing ğ•œ E).flip
#align weak_space WeakSpace

namespace WeakSpace

-- Porting note: the next four instances should be derived from the definition
instance instAddCommMonoid : AddCommMonoid (WeakSpace ğ•œ E) :=
  WeakBilin.instAddCommMonoid (topDualPairing ğ•œ E).flip

instance instModule : Module ğ•œ (WeakSpace ğ•œ E) :=
  WeakBilin.instModule (topDualPairing ğ•œ E).flip

instance instTopologicalSpace : TopologicalSpace (WeakSpace ğ•œ E) :=
  WeakBilin.instTopologicalSpace (topDualPairing ğ•œ E).flip

instance instContinuousAdd : ContinuousAdd (WeakSpace ğ•œ E) :=
  WeakBilin.instContinuousAdd (topDualPairing ğ•œ E).flip

variable [AddCommMonoid F] [Module ğ•œ F] [TopologicalSpace F]

/-- A continuous linear map from `E` to `F` is still continuous when `E` and `F` are equipped with
their weak topologies. -/
def map (f : E â†’L[ğ•œ] F) : WeakSpace ğ•œ E â†’L[ğ•œ] WeakSpace ğ•œ F :=
  { f with
    cont :=
      WeakBilin.continuous_of_continuous_eval _ fun l => WeakBilin.eval_continuous _ (l âˆ˜L f) }
#align weak_space.map WeakSpace.map

theorem map_apply (f : E â†’L[ğ•œ] F) (x : E) : WeakSpace.map f x = f x :=
  rfl
#align weak_space.map_apply WeakSpace.map_apply

@[simp]
theorem coe_map (f : E â†’L[ğ•œ] F) : (WeakSpace.map f : E â†’ F) = f :=
  rfl
#align weak_space.coe_map WeakSpace.coe_map

end WeakSpace

variable (ğ•œ E) in
/-- There is a canonical map `E â†’ WeakSpace ğ•œ E` (the "identity"
mapping). It is a linear equivalence. -/
def toWeakSpace : E â‰ƒâ‚—[ğ•œ] WeakSpace ğ•œ E := LinearEquiv.refl ğ•œ E

variable (ğ•œ E) in
/-- For a topological vector space `E`, "identity mapping" `E â†’ WeakSpace ğ•œ E` is continuous.
This definition implements it as a continuous linear map. -/
def continuousLinearMapToWeakSpace : E â†’L[ğ•œ] WeakSpace ğ•œ E where
  __ := toWeakSpace ğ•œ E
  cont := by
    apply WeakBilin.continuous_of_continuous_eval
    exact ContinuousLinearMap.continuous

variable (ğ•œ E) in
@[simp]
theorem continuousLinearMapToWeakSpace_eq_toWeakSpace (x : E) :
    continuousLinearMapToWeakSpace ğ•œ E x = toWeakSpace ğ•œ E x := by rfl

theorem continuousLinearMapToWeakSpace_bijective :
    Function.Bijective (continuousLinearMapToWeakSpace ğ•œ E) :=
  (toWeakSpace ğ•œ E).bijective

/-- The canonical map from `WeakSpace ğ•œ E` to `E` is an open map. -/
theorem isOpenMap_toWeakSpace_symm : IsOpenMap (toWeakSpace ğ•œ E).symm :=
  IsOpenMap.of_inverse (continuousLinearMapToWeakSpace ğ•œ E).cont
    (toWeakSpace ğ•œ E).left_inv (toWeakSpace ğ•œ E).right_inv

/-- A set in `E` which is open in the weak topology is open. -/
theorem WeakSpace.isOpen_of_isOpen (V : Set E)
    (hV : IsOpen ((continuousLinearMapToWeakSpace ğ•œ E) '' V : Set (WeakSpace ğ•œ E))) : IsOpen V := by
  simpa [Set.image_image] using isOpenMap_toWeakSpace_symm _ hV

theorem tendsto_iff_forall_eval_tendsto_topDualPairing {l : Filter Î±} {f : Î± â†’ WeakDual ğ•œ E}
    {x : WeakDual ğ•œ E} :
    Tendsto f l (ğ“ x) â†”
      âˆ€ y, Tendsto (fun i => topDualPairing ğ•œ E (f i) y) l (ğ“ (topDualPairing ğ•œ E x y)) :=
  WeakBilin.tendsto_iff_forall_eval_tendsto _ ContinuousLinearMap.coe_injective
#align tendsto_iff_forall_eval_tendsto_top_dual_pairing tendsto_iff_forall_eval_tendsto_topDualPairing

end WeakStarTopology
