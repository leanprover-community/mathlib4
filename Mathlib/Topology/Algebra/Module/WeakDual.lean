/-
Copyright (c) 2021 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤, Moritz Doll
-/
--import Mathlib.Topology.Algebra.Module.Basic
import Mathlib.LinearAlgebra.BilinearMap
import Mathlib.Topology.Algebra.Module.Dual

#align_import topology.algebra.module.weak_dual from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Weak dual topology

This file defines the weak topology given two vector spaces `E` and `F` over a commutative semiring
`ğ•œ` and a bilinear form `B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ`. The weak topology on `E` is the coarsest topology
such that for all `y : F` every map `fun x => B x y` is continuous.

In the case that `F = E â†’L[ğ•œ] ğ•œ` and `B` being the canonical pairing, we obtain the weak-* topology,
`WeakDual ğ•œ E := (E â†’L[ğ•œ] ğ•œ)`. Interchanging the arguments in the bilinear form yields the
weak topology `WeakSpace ğ•œ E := E`.

## Main definitions

The main definitions are the types `WeakBilin B` for the general case and the two special cases
`WeakDual ğ•œ E` and `WeakSpace ğ•œ E` with the respective topology instances on it.

* Given `B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ`, the type `WeakBilin B` is a type synonym for `E`.
* The instance `WeakBilin.instTopologicalSpace` is the weak topology induced by the bilinear form
  `B`.
* `WeakDual ğ•œ E` is a type synonym for `Dual ğ•œ E`: both are equal to the type `E â†’L[ğ•œ] ğ•œ` of
  continuous linear maps from a module `E` over `ğ•œ` to the ring `ğ•œ`.
* The instance `WeakDual.instTopologicalSpace` is the weak-* topology on `WeakDual ğ•œ E`, i.e., the
  coarsest topology making the evaluation maps at all `z : E` continuous.
* `WeakSpace ğ•œ E` is a type synonym for `E`.
* The instance `WeakSpace.instTopologicalSpace` is the weak topology on `E`, i.e., the
  coarsest topology such that all `v : Dual ğ•œ E` remain continuous.

## Main results

We establish that `WeakBilin B` has the following structure:
* `WeakBilin.instContinuousAdd`: The addition in `WeakBilin B` is continuous.
* `WeakBilin.instContinuousSMul`: The scalar multiplication in `WeakBilin B` is continuous.

We prove the following results characterizing the weak topology:
* `evalCLM`: For any `y : F`, the evaluation mapping `x â†¦ B x y` is continuous.
* `continuous_of_continuous_eval`: For a mapping to `WeakBilin B` to be continuous,
  it suffices that its compositions with pairing with `B` at all points `y : F` is continuous.
* `tendsto_iff_forall_eval_tendsto`: Convergence in `WeakBilin B` can be characterized
  in terms of convergence of the evaluations at all points `y : F`.

## References

* [H. H. Schaefer, *Topological Vector Spaces*][schaefer1966]

## Tags

weak-star, weak dual, duality

-/


noncomputable section

open Filter

open Topology

variable {Î± ğ•œ ğ• R E F M : Type*}

/-- The space `E` equipped with the weak topology induced by the bilinear form `B`. -/
@[nolint unusedArguments]
def WeakBilin [CommSemiring ğ•œ] [AddCommMonoid E] [Module ğ•œ E] [AddCommMonoid F] [Module ğ•œ F]
    (_ : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) := E
#align weak_bilin WeakBilin

namespace WeakBilin

instance instAddCommMonoid [CommSemiring ğ•œ] [a : AddCommMonoid E] [Module ğ•œ E] [AddCommMonoid F]
    [Module ğ•œ F] (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) : AddCommMonoid (WeakBilin B) := a

instance instModule [CommSemiring ğ•œ] [AddCommMonoid E] [m : Module ğ•œ E] [AddCommMonoid F]
    [Module ğ•œ F] (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) : Module ğ•œ (WeakBilin B) := m

instance instAddCommGroup [CommSemiring ğ•œ] [a : AddCommGroup E] [Module ğ•œ E] [AddCommMonoid F]
    [Module ğ•œ F] (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) : AddCommGroup (WeakBilin B) := a

instance (priority := 100) instModule' [CommSemiring ğ•œ] [CommSemiring ğ•] [AddCommGroup E]
    [Module ğ•œ E] [AddCommGroup F] [Module ğ•œ F] [m : Module ğ• E] (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) :
    Module ğ• (WeakBilin B) := m
#align weak_bilin.module' WeakBilin.instModule'

instance instIsScalarTower [CommSemiring ğ•œ] [CommSemiring ğ•] [AddCommGroup E] [Module ğ•œ E]
    [AddCommGroup F] [Module ğ•œ F] [SMul ğ• ğ•œ] [Module ğ• E] [s : IsScalarTower ğ• ğ•œ E]
    (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) : IsScalarTower ğ• ğ•œ (WeakBilin B) := s

section Semiring

variable [TopologicalSpace ğ•œ] [CommSemiring ğ•œ]
variable [AddCommMonoid E] [Module ğ•œ E]
variable [AddCommMonoid F] [Module ğ•œ F]
variable (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ)

instance instTopologicalSpace : TopologicalSpace (WeakBilin B) :=
  TopologicalSpace.induced (B Â· Â·) Pi.topologicalSpace

/-- The coercion `B : E â†’ (F â†’ ğ•œ)` as a continuous linear map. -/
def coeFnCLM : WeakBilin B â†’L[ğ•œ] (F â†’ ğ•œ) where
  toFun := (B Â· Â·)
  map_add' := fun _ _ â†¦ by ext; simp
  map_smul' := fun _ _ â†¦ by ext; simp
  cont := continuous_induced_dom
#align weak_bilin.coe_fn_continuous WeakBilin.coeFnCLM

@[simp]
theorem coeFnCLM_apply (x : WeakBilin B) : coeFnCLM B x = B x := rfl

/-- The map `x â†¦ B x y` for fixed `y : F` as a continuous linear map. -/
def evalCLM (y : F) : WeakBilin B â†’L[ğ•œ] ğ•œ where
  toLinearMap := B.flip y
  cont := (continuous_pi_iff.mp (coeFnCLM B).continuous) y
#align weak_bilin.eval_continuous WeakBilin.evalCLM

@[simp]
theorem evalCLM_apply (y : F) (x : WeakBilin B) : evalCLM B y x = B x y := rfl

theorem continuous_of_continuous_eval [TopologicalSpace Î±] {g : Î± â†’ WeakBilin B}
    (h : âˆ€ y, Continuous fun a => B (g a) y) : Continuous g :=
  continuous_induced_rng.2 (continuous_pi_iff.mpr h)
#align weak_bilin.continuous_of_continuous_eval WeakBilin.continuous_of_continuous_eval

/-- The coercion `(x y â†¦ B x y) : E â†’ (F â†’ ğ•œ)` is an embedding. -/
theorem embedding {B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ} (hB : Function.Injective B) :
    Embedding fun (x : WeakBilin B) y â†¦ B x y :=
  Function.Injective.embedding_induced <| LinearMap.coe_injective.comp hB
#align weak_bilin.embedding WeakBilin.embedding

theorem tendsto_iff_forall_eval_tendsto {l : Filter Î±} {f : Î± â†’ WeakBilin B} {x : WeakBilin B}
    (hB : Function.Injective B) :
    Tendsto f l (ğ“ x) â†” âˆ€ y, Tendsto (fun i => B (f i) y) l (ğ“ (B x y)) := by
  rw [â† tendsto_pi_nhds, Embedding.tendsto_nhds_iff (embedding hB)]
  rfl
#align weak_bilin.tendsto_iff_forall_eval_tendsto WeakBilin.tendsto_iff_forall_eval_tendsto

/-- Addition in `WeakBilin B` is continuous. -/
instance instContinuousAdd [ContinuousAdd ğ•œ] : ContinuousAdd (WeakBilin B) := by
  refine âŸ¨continuous_induced_rng.2 ?_âŸ©
  refine cast (congr_arg _ ?_)
    (((coeFnCLM B).continuous.comp continuous_fst).add
      ((coeFnCLM B).continuous.comp continuous_snd))
  ext
  simp

/-- Scalar multiplication by `ğ•œ` on `WeakBilin B` is continuous. -/
instance instContinuousSMul [ContinuousSMul ğ•œ ğ•œ] : ContinuousSMul ğ•œ (WeakBilin B) := by
  refine âŸ¨continuous_induced_rng.2 ?_âŸ©
  refine cast (congr_arg _ ?_) (continuous_fst.smul ((coeFnCLM B).continuous.comp continuous_snd))
  ext
  simp

end Semiring

section Ring

variable [TopologicalSpace ğ•œ] [CommRing ğ•œ]
variable [AddCommGroup E] [Module ğ•œ E]
variable [AddCommGroup F] [Module ğ•œ F]

variable (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ)

/-- `WeakBilin B` is a `TopologicalAddGroup`, meaning that addition and negation are
continuous. -/
instance instTopologicalAddGroup [ContinuousAdd ğ•œ] : TopologicalAddGroup (WeakBilin B) where
  toContinuousAdd := by infer_instance
  continuous_neg := by
    apply continuous_of_continuous_eval
    intro y
    convert (evalCLM B (-y)).continuous
    simp

end Ring

end WeakBilin

section WeakStarTopology

variable [CommSemiring ğ•œ] [TopologicalSpace ğ•œ] [ContinuousAdd ğ•œ] [AddCommMonoid E] [Module ğ•œ E]
  [TopologicalSpace E] [ContinuousConstSMul ğ•œ ğ•œ]

variable (ğ•œ E) in
/-- The weak-* topology is the topology coarsest topology on `Dual ğ•œ E` such that all
functionals `v â†¦ v x` are continuous. -/
@[reducible]
def WeakDual : Type _ := WeakBilin (dualPairing ğ•œ E)
#align weak_dual WeakDual

namespace WeakDual

instance instInhabited : Inhabited (WeakDual ğ•œ E) :=
  ContinuousLinearMap.inhabited

instance instFunLike : FunLike (WeakDual ğ•œ E) E ğ•œ :=
  ContinuousLinearMap.funLike

instance instContinuousLinearMapClass : ContinuousLinearMapClass (WeakDual ğ•œ E) ğ•œ E ğ•œ :=
  ContinuousLinearMap.continuousSemilinearMapClass
#align weak_dual.weak_dual.continuous_linear_map_class WeakDual.instContinuousLinearMapClass

/-- Helper instance for when there's too many metavariables to apply `DFunLike.hasCoeToFun`
directly. -/
instance instCoeFun : CoeFun (WeakDual ğ•œ E) fun _ => E â†’ ğ•œ :=
  DFunLike.hasCoeToFun

variable (ğ•œ E) in
/-- The coercion `WeakDual ğ•œ E â†’ (E â†’ ğ•œ)` as a continuous linear map. -/
def coeFnCLM : WeakDual ğ•œ E â†’L[ğ•œ] (E â†’ ğ•œ) := WeakBilin.coeFnCLM (dualPairing ğ•œ E)
#align weak_dual.coe_fn_continuous WeakDual.coeFnCLM

@[simp]
theorem coeFnCLM_apply (x : WeakDual ğ•œ E) : coeFnCLM ğ•œ E x = dualPairing ğ•œ E x := rfl

variable (ğ•œ) in
/-- The map `x â†¦ x y` for fixed `y : E` as a continuous linear map. -/
def evalCLM (y : E) : WeakDual ğ•œ E â†’L[ğ•œ] ğ•œ := WeakBilin.evalCLM (dualPairing ğ•œ E) y
#align weak_dual.eval_continuous WeakDual.evalCLM

@[simp]
theorem evalCLM_apply (y : E) (x : WeakDual ğ•œ E) : evalCLM ğ•œ y x = x y := rfl

/-- If a monoid `M` distributively continuously acts on `ğ•œ` and this action commutes with
multiplication on `ğ•œ`, then it acts on `WeakDual ğ•œ E`. -/
instance instMulAction (M) [Monoid M] [DistribMulAction M ğ•œ] [SMulCommClass ğ•œ M ğ•œ]
    [ContinuousConstSMul M ğ•œ] : MulAction M (WeakDual ğ•œ E) :=
  ContinuousLinearMap.mulAction

/-- If a monoid `M` distributively continuously acts on `ğ•œ` and this action commutes with
multiplication on `ğ•œ`, then it acts distributively on `WeakDual ğ•œ E`. -/
instance instDistribMulAction (M) [Monoid M] [DistribMulAction M ğ•œ] [SMulCommClass ğ•œ M ğ•œ]
    [ContinuousConstSMul M ğ•œ] : DistribMulAction M (WeakDual ğ•œ E) :=
  ContinuousLinearMap.distribMulAction

/-- If `ğ•œ` is a topological module over a semiring `R` and scalar multiplication commutes with the
multiplication on `ğ•œ`, then `WeakDual ğ•œ E` is a module over `R`. -/
instance instModule' (R) [Semiring R] [Module R ğ•œ] [SMulCommClass ğ•œ R ğ•œ] [ContinuousConstSMul R ğ•œ] :
    Module R (WeakDual ğ•œ E) :=
  ContinuousLinearMap.module
#align weak_dual.module' WeakDual.instModule'

instance instContinuousConstSMul (M) [Monoid M] [DistribMulAction M ğ•œ] [SMulCommClass ğ•œ M ğ•œ]
    [ContinuousConstSMul M ğ•œ] : ContinuousConstSMul M (WeakDual ğ•œ E) :=
  âŸ¨(continuous_induced_rng.2 <| (WeakDual.coeFnCLM ğ•œ E).continuous.const_smul Â·)âŸ©

/-- If a monoid `M` distributively continuously acts on `ğ•œ` and this action commutes with
multiplication on `ğ•œ`, then it continuously acts on `WeakDual ğ•œ E`. -/
instance instContinuousSMul (M) [Monoid M] [DistribMulAction M ğ•œ] [SMulCommClass ğ•œ M ğ•œ]
    [TopologicalSpace M] [ContinuousSMul M ğ•œ] : ContinuousSMul M (WeakDual ğ•œ E) :=
  âŸ¨continuous_induced_rng.2 <|
      continuous_fst.smul ((WeakDual.coeFnCLM ğ•œ E).continuous.comp continuous_snd)âŸ©

theorem continuous_of_continuous_eval [TopologicalSpace Î±] {g : Î± â†’ WeakDual ğ•œ E}
    (h : âˆ€ y, Continuous fun a => (g a) y) : Continuous g :=
  continuous_induced_rng.2 (continuous_pi_iff.mpr h)
#align weak_dual.continuous_of_continuous_eval WeakDual.continuous_of_continuous_eval

variable (ğ•œ E) in
/-- The coercion `WeakDual ğ•œ E â†’ (E â†’ ğ•œ)` is an embedding. -/
theorem embedding :
    Embedding fun (x : WeakDual ğ•œ E) y â†¦ x y :=
  Function.Injective.embedding_induced <| LinearMap.coe_injective.comp
    ContinuousLinearMap.coe_injective

theorem tendsto_iff_forall_eval_tendsto_dualPairing {l : Filter Î±} {f : Î± â†’ WeakDual ğ•œ E}
    {x : WeakDual ğ•œ E} : Tendsto f l (ğ“ x) â†” âˆ€ y, Tendsto (f Â· y) l (ğ“ (x y)) :=
  WeakBilin.tendsto_iff_forall_eval_tendsto _ ContinuousLinearMap.coe_injective
#align tendsto_iff_forall_eval_tendsto_top_dual_pairing WeakDual.tendsto_iff_forall_eval_tendsto_dualPairing

instance instT2Space [T2Space ğ•œ] : T2Space (WeakDual ğ•œ E) :=
  Embedding.t2Space <|
    WeakBilin.embedding <|
      show Function.Injective (dualPairing ğ•œ E) from ContinuousLinearMap.coe_injective

end WeakDual

/-- The weak topology is the topology coarsest topology on `E` such that all functionals
`fun x â†¦ v x` are continuous. -/
@[reducible]
def WeakSpace (ğ•œ E) [CommSemiring ğ•œ] [TopologicalSpace ğ•œ] [ContinuousAdd ğ•œ]
    [ContinuousConstSMul ğ•œ ğ•œ] [AddCommMonoid E] [Module ğ•œ E] [TopologicalSpace E] :=
  WeakBilin (dualPairing ğ•œ E).flip
#align weak_space WeakSpace

namespace WeakSpace

variable (ğ•œ E) in
/-- The coercion `E â†’ Dual ğ•œ E` as a continuous linear map in the weak topology. -/
def coeFnCLM : WeakSpace ğ•œ E â†’L[ğ•œ] (Dual ğ•œ E â†’ ğ•œ) := WeakBilin.coeFnCLM (dualPairing ğ•œ E).flip

@[simp]
theorem coeFnCLM_apply (x : WeakSpace ğ•œ E) : coeFnCLM ğ•œ E x = (dualPairing ğ•œ E).flip x := rfl

/-- The map `x â†¦ y x` for fixed `y : Dual ğ•œ E` as a continuous linear map. -/
def evalCLM (y : Dual ğ•œ E) : WeakSpace ğ•œ E â†’L[ğ•œ] ğ•œ := WeakBilin.evalCLM (dualPairing ğ•œ E).flip y

@[simp]
theorem evalCLM_apply (y : Dual ğ•œ E) (x : WeakSpace ğ•œ E) : evalCLM y x = y x := rfl

variable [AddCommMonoid F] [Module ğ•œ F] [TopologicalSpace F]

/-- A continuous linear map from `E` to `F` is still continuous when `E` and `F` are equipped with
their weak topologies. -/
def map (f : E â†’L[ğ•œ] F) : WeakSpace ğ•œ E â†’L[ğ•œ] WeakSpace ğ•œ F :=
  { f with
    cont :=
      WeakBilin.continuous_of_continuous_eval _ fun l â†¦ (WeakBilin.evalCLM _ (l âˆ˜L f)).continuous }
#align weak_space.map WeakSpace.map

theorem map_apply (f : E â†’L[ğ•œ] F) (x : E) : WeakSpace.map f x = f x :=
  rfl
#align weak_space.map_apply WeakSpace.map_apply

@[simp]
theorem coe_map (f : E â†’L[ğ•œ] F) : (WeakSpace.map f : E â†’ F) = f :=
  rfl
#align weak_space.coe_map WeakSpace.coe_map

end WeakSpace

end WeakStarTopology
