/-
Copyright (c) 2023 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.SetTheory.Cardinal.CountableCover
import Mathlib.Data.Real.Cardinality
import Mathlib.Analysis.SpecificLimits.Normed

/-!
# Cardinality of open subsets of vector spaces

Any nonempty open subset of a topological vector space over a nontrivially normed field has the same
cardinality as the whole space. This is proved in `cardinal_eq_of_is_open`.

We deduce that a countable set in a nontrivial real vector space has dense complement, in
`Set.Countable.dense_compl`.
-/
universe u v

open Filter Pointwise Set
open scoped Cardinal Topology

/-- In a topological vector space over a nontrivially normed field, any neighborhood of zero has
the same cardinality as the whole space. -/
lemma cardinal_eq_of_mem_nhds_zero
    {E : Type _} (ğ•œ : Type _) [NontriviallyNormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E]
    [TopologicalSpace E] [ContinuousSMul ğ•œ E] {s : Set E} (hs : s âˆˆ ğ“ (0 : E)) : #s = #E := by
  /- As `s` is a neighborhood of `0`, the space is covered by the rescaled sets `c^n â€¢ s`,
  where `c` is any element of `ğ•œ` with norm `> 1`. All these sets are in bijection and have
  therefore the same cardinality. The conclusion follows. -/
  obtain âŸ¨c, hcâŸ© : âˆƒ x : ğ•œ , 1 < â€–xâ€– := NormedField.exists_lt_norm ğ•œ 1
  have cn_ne : âˆ€ n, c^n â‰  0 := by
    intro n
    apply pow_ne_zero
    rintro rfl
    simp only [norm_zero] at hc
    exact lt_irrefl _ (hc.trans zero_lt_one)
  have A : âˆ€ (x : E), âˆ€á¶  n in (atTop : Filter â„•), x âˆˆ c^n â€¢ s := by
    intro x
    have : Tendsto (fun n â†¦ (c^n) â»Â¹ â€¢ x) atTop (ğ“ ((0 : ğ•œ) â€¢ x)) := by
      have : Tendsto (fun n â†¦ (c^n)â»Â¹) atTop (ğ“ 0) := by
        simp_rw [â† inv_pow]
        apply tendsto_pow_atTop_nhds_0_of_norm_lt_1
        rw [norm_inv]
        exact inv_lt_one hc
      exact Tendsto.smul_const this x
    rw [zero_smul] at this
    filter_upwards [this hs] with n (hn : (c ^ n)â»Â¹ â€¢ x âˆˆ s)
    exact (mem_smul_set_iff_inv_smul_memâ‚€ (cn_ne n) _ _).2 hn
  have B : âˆ€ n, #(c^n â€¢ s) = #s := by
    intro n
    have : c^n â€¢ s â‰ƒ s :=
    { toFun := fun x â†¦ âŸ¨(c^n)â»Â¹ â€¢ x.1, (mem_smul_set_iff_inv_smul_memâ‚€ (cn_ne n) _ _).1 x.2âŸ©
      invFun := fun x â†¦ âŸ¨(c^n) â€¢ x.1, smul_mem_smul_set x.2âŸ©
      left_inv := fun x â†¦ by simp [smul_smul, mul_inv_cancel (cn_ne n)]
      right_inv := fun x â†¦ by simp [smul_smul, inv_mul_cancel (cn_ne n)] }
    exact Cardinal.mk_congr this
  apply (Cardinal.mk_of_countable_eventually_mem A B).symm

/-- In a topological vector space over a nontrivially normed field, any neighborhood of a point has
the same cardinality as the whole space. -/
theorem cardinal_eq_of_mem_nhds
    {E : Type _} (ğ•œ : Type _) [NontriviallyNormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E]
    [TopologicalSpace E] [ContinuousAdd E] [ContinuousSMul ğ•œ E]
    {s : Set E} {x : E} (hs : s âˆˆ ğ“ x) : #s = #E := by
  let g := Homeomorph.addLeft x
  let t := g â»Â¹' s
  have : t âˆˆ ğ“ 0 := g.continuous.continuousAt.preimage_mem_nhds (by simpa using hs)
  have A : #t = #E := cardinal_eq_of_mem_nhds_zero ğ•œ this
  have B : #t = #s := Cardinal.mk_subtype_of_equiv s g.toEquiv
  rwa [B] at A

/-- In a topological vector space over a nontrivially normed field, any nonempty open set has
the same cardinality as the whole space. -/
theorem cardinal_eq_of_is_open
    {E : Type _} (ğ•œ : Type _) [NontriviallyNormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E]
    [TopologicalSpace E] [ContinuousAdd E] [ContinuousSMul ğ•œ E] {s : Set E}
    (hs : IsOpen s) (h's : s.Nonempty) : #s = #E := by
  rcases h's with âŸ¨x, hxâŸ©
  exact cardinal_eq_of_mem_nhds ğ•œ (hs.mem_nhds hx)

/-- In a nontrivial real topological vector space, a countable subset has dense complement. -/
theorem Set.Countable.dense_compl {E : Type u} [AddCommGroup E] [Module â„ E]
    [TopologicalSpace E] [ContinuousAdd E] [ContinuousSMul â„ E]
    [Nontrivial E] (s : Set E) (hs : s.Countable) : Dense sá¶œ := by
  rw [â† interior_eq_empty_iff_dense_compl]
  contrapose! hs
  intro H
  apply lt_irrefl (â„µâ‚€ : Cardinal.{u})
  calc
  â„µâ‚€ < Cardinal.lift.{u} (#â„) := by simp [Cardinal.mk_real, Cardinal.aleph0_lt_continuum]
  _  â‰¤ #E := by simpa using Cardinal.mk_le_of_module â„ E
  _  = #(interior s) := (cardinal_eq_of_is_open â„ isOpen_interior (nmem_singleton_empty.1 hs)).symm
  _  â‰¤ â„µâ‚€ := (H.mono interior_subset).le_aleph0
