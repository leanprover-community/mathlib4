/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, SÃ©bastien GouÃ«zel, Jean Lo, Yury Kudryashov, FrÃ©dÃ©ric Dupuis,
  Heather Macbeth
-/
import Mathlib.Topology.Algebra.Module.LinearMapPiProd

/-!
# Continuous linear equivalences

Continuous semilinear / linear / star-linear equivalences between topological modules are denoted
by `M â‰ƒSL[Ïƒ] Mâ‚‚`, `M â‰ƒL[R] Mâ‚‚` and `M â‰ƒLâ‹†[R] Mâ‚‚`.
-/

assert_not_exists TrivialStar

open LinearMap (ker range)
open Topology Filter Pointwise

universe u v w u'

section

/-- Continuous linear equivalences between modules. We only put the type classes that are necessary
for the definition, although in applications `M` and `Mâ‚‚` will be topological modules over the
topological semiring `R`. -/
structure ContinuousLinearEquiv {R : Type*} {S : Type*} [Semiring R] [Semiring S] (Ïƒ : R â†’+* S)
    {Ïƒ' : S â†’+* R} [RingHomInvPair Ïƒ Ïƒ'] [RingHomInvPair Ïƒ' Ïƒ] (M : Type*) [TopologicalSpace M]
    [AddCommMonoid M] (Mâ‚‚ : Type*) [TopologicalSpace Mâ‚‚] [AddCommMonoid Mâ‚‚] [Module R M]
    [Module S Mâ‚‚] extends M â‰ƒâ‚›â‚—[Ïƒ] Mâ‚‚ where
  continuous_toFun : Continuous toFun := by continuity
  continuous_invFun : Continuous invFun := by continuity

attribute [inherit_doc ContinuousLinearEquiv] ContinuousLinearEquiv.continuous_toFun
ContinuousLinearEquiv.continuous_invFun

@[inherit_doc]
notation:50 M " â‰ƒSL[" Ïƒ "] " Mâ‚‚ => ContinuousLinearEquiv Ïƒ M Mâ‚‚

@[inherit_doc]
notation:50 M " â‰ƒL[" R "] " Mâ‚‚ => ContinuousLinearEquiv (RingHom.id R) M Mâ‚‚

/-- `ContinuousSemilinearEquivClass F Ïƒ M Mâ‚‚` asserts `F` is a type of bundled continuous
`Ïƒ`-semilinear equivs `M â†’ Mâ‚‚`.  See also `ContinuousLinearEquivClass F R M Mâ‚‚` for the case
where `Ïƒ` is the identity map on `R`.  A map `f` between an `R`-module and an `S`-module over a ring
homomorphism `Ïƒ : R â†’+* S` is semilinear if it satisfies the two properties `f (x + y) = f x + f y`
and `f (c â€¢ x) = (Ïƒ c) â€¢ f x`. -/
class ContinuousSemilinearEquivClass (F : Type*) {R : outParam Type*} {S : outParam Type*}
    [Semiring R] [Semiring S] (Ïƒ : outParam <| R â†’+* S) {Ïƒ' : outParam <| S â†’+* R}
    [RingHomInvPair Ïƒ Ïƒ'] [RingHomInvPair Ïƒ' Ïƒ] (M : outParam Type*) [TopologicalSpace M]
    [AddCommMonoid M] (Mâ‚‚ : outParam Type*) [TopologicalSpace Mâ‚‚] [AddCommMonoid Mâ‚‚] [Module R M]
    [Module S Mâ‚‚] [EquivLike F M Mâ‚‚] : Prop extends SemilinearEquivClass F Ïƒ M Mâ‚‚ where
  map_continuous : âˆ€ f : F, Continuous f := by continuity
  inv_continuous : âˆ€ f : F, Continuous (EquivLike.inv f) := by continuity

attribute [inherit_doc ContinuousSemilinearEquivClass]
ContinuousSemilinearEquivClass.map_continuous
ContinuousSemilinearEquivClass.inv_continuous

/-- `ContinuousLinearEquivClass F Ïƒ M Mâ‚‚` asserts `F` is a type of bundled continuous
`R`-linear equivs `M â†’ Mâ‚‚`. This is an abbreviation for
`ContinuousSemilinearEquivClass F (RingHom.id R) M Mâ‚‚`. -/
abbrev ContinuousLinearEquivClass (F : Type*) (R : outParam Type*) [Semiring R]
    (M : outParam Type*) [TopologicalSpace M] [AddCommMonoid M] (Mâ‚‚ : outParam Type*)
    [TopologicalSpace Mâ‚‚] [AddCommMonoid Mâ‚‚] [Module R M] [Module R Mâ‚‚] [EquivLike F M Mâ‚‚] :=
  ContinuousSemilinearEquivClass F (RingHom.id R) M Mâ‚‚

namespace ContinuousSemilinearEquivClass

variable (F : Type*) {R : Type*} {S : Type*} [Semiring R] [Semiring S] (Ïƒ : R â†’+* S)
  {Ïƒ' : S â†’+* R} [RingHomInvPair Ïƒ Ïƒ'] [RingHomInvPair Ïƒ' Ïƒ]
  (M : Type*) [TopologicalSpace M] [AddCommMonoid M]
  (Mâ‚‚ : Type*) [TopologicalSpace Mâ‚‚] [AddCommMonoid Mâ‚‚]
  [Module R M] [Module S Mâ‚‚]

-- `Ïƒ'` becomes a metavariable, but it's OK since it's an outparam
instance (priority := 100) continuousSemilinearMapClass [EquivLike F M Mâ‚‚]
    [s : ContinuousSemilinearEquivClass F Ïƒ M Mâ‚‚] : ContinuousSemilinearMapClass F Ïƒ M Mâ‚‚ :=
  { s with }

instance (priority := 100) [EquivLike F M Mâ‚‚]
    [s : ContinuousSemilinearEquivClass F Ïƒ M Mâ‚‚] : HomeomorphClass F M Mâ‚‚ :=
  { s with }

end ContinuousSemilinearEquivClass

namespace ContinuousLinearMap

section Pi

variable {R : Type*} [Semiring R] {M : Type*} [TopologicalSpace M] [AddCommMonoid M] [Module R M]
  {Mâ‚‚ : Type*} [TopologicalSpace Mâ‚‚] [AddCommMonoid Mâ‚‚] [Module R Mâ‚‚] {Î¹ : Type*} {Ï† : Î¹ â†’ Type*}
  [âˆ€ i, TopologicalSpace (Ï† i)] [âˆ€ i, AddCommMonoid (Ï† i)] [âˆ€ i, Module R (Ï† i)]

variable (R Ï†)

/-- If `I` and `J` are complementary index sets, the product of the kernels of the `J`th projections
of `Ï†` is linearly equivalent to the product over `I`. -/
def iInfKerProjEquiv {I J : Set Î¹} [DecidablePred fun i => i âˆˆ I] (hd : Disjoint I J)
    (hu : Set.univ âŠ† I âˆª J) :
    (â¨… i âˆˆ J, ker (proj i : (âˆ€ i, Ï† i) â†’L[R] Ï† i) :
    Submodule R (âˆ€ i, Ï† i)) â‰ƒL[R] âˆ€ i : I, Ï† i where
  toLinearEquiv := LinearMap.iInfKerProjEquiv R Ï† hd hu
  continuous_toFun :=
    continuous_pi fun i =>
      Continuous.comp (continuous_apply (A := Ï†) i) <|
        @continuous_subtype_val _ _ fun x =>
          x âˆˆ (â¨… i âˆˆ J, ker (proj i : (âˆ€ i, Ï† i) â†’L[R] Ï† i) : Submodule R (âˆ€ i, Ï† i))
  continuous_invFun :=
    Continuous.subtype_mk
      (continuous_pi fun i => by
        dsimp
        split_ifs <;> [apply continuous_apply; exact continuous_zero])
      _

end Pi

end ContinuousLinearMap

namespace ContinuousLinearEquiv

section AddCommMonoid

variable {Râ‚ : Type*} {Râ‚‚ : Type*} {Râ‚ƒ : Type*} [Semiring Râ‚] [Semiring Râ‚‚] [Semiring Râ‚ƒ]
  {Ïƒâ‚â‚‚ : Râ‚ â†’+* Râ‚‚} {Ïƒâ‚‚â‚ : Râ‚‚ â†’+* Râ‚} [RingHomInvPair Ïƒâ‚â‚‚ Ïƒâ‚‚â‚] [RingHomInvPair Ïƒâ‚‚â‚ Ïƒâ‚â‚‚]
  {Ïƒâ‚‚â‚ƒ : Râ‚‚ â†’+* Râ‚ƒ} {Ïƒâ‚ƒâ‚‚ : Râ‚ƒ â†’+* Râ‚‚} [RingHomInvPair Ïƒâ‚‚â‚ƒ Ïƒâ‚ƒâ‚‚] [RingHomInvPair Ïƒâ‚ƒâ‚‚ Ïƒâ‚‚â‚ƒ]
  {Ïƒâ‚â‚ƒ : Râ‚ â†’+* Râ‚ƒ} {Ïƒâ‚ƒâ‚ : Râ‚ƒ â†’+* Râ‚} [RingHomInvPair Ïƒâ‚â‚ƒ Ïƒâ‚ƒâ‚] [RingHomInvPair Ïƒâ‚ƒâ‚ Ïƒâ‚â‚ƒ]
  [RingHomCompTriple Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ Ïƒâ‚â‚ƒ] [RingHomCompTriple Ïƒâ‚ƒâ‚‚ Ïƒâ‚‚â‚ Ïƒâ‚ƒâ‚] {Mâ‚ : Type*}
  [TopologicalSpace Mâ‚] [AddCommMonoid Mâ‚]
  {Mâ‚‚ : Type*} [TopologicalSpace Mâ‚‚] [AddCommMonoid Mâ‚‚] {Mâ‚ƒ : Type*} [TopologicalSpace Mâ‚ƒ]
  [AddCommMonoid Mâ‚ƒ] {Mâ‚„ : Type*} [TopologicalSpace Mâ‚„] [AddCommMonoid Mâ‚„] [Module Râ‚ Mâ‚]
  [Module Râ‚‚ Mâ‚‚] [Module Râ‚ƒ Mâ‚ƒ]

/-- A continuous linear equivalence induces a continuous linear map. -/
@[coe]
def toContinuousLinearMap (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) : Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚ :=
  { e.toLinearEquiv.toLinearMap with cont := e.continuous_toFun }

/-- Coerce continuous linear equivs to continuous linear maps. -/
instance ContinuousLinearMap.coe : Coe (Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) (Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚) :=
  âŸ¨toContinuousLinearMapâŸ©

instance equivLike :
    EquivLike (Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) Mâ‚ Mâ‚‚ where
  coe f := f.toFun
  inv f := f.invFun
  coe_injective' f g hâ‚ hâ‚‚ := by
    obtain âŸ¨f', _âŸ© := f
    obtain âŸ¨g', _âŸ© := g
    rcases f' with âŸ¨âŸ¨âŸ¨_, _âŸ©, _âŸ©, _âŸ©
    rcases g' with âŸ¨âŸ¨âŸ¨_, _âŸ©, _âŸ©, _âŸ©
    congr
  left_inv f := f.left_inv
  right_inv f := f.right_inv

instance continuousSemilinearEquivClass :
    ContinuousSemilinearEquivClass (Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) Ïƒâ‚â‚‚ Mâ‚ Mâ‚‚ where
  map_add f := f.map_add'
  map_smulâ‚›â‚— f := f.map_smul'
  map_continuous := continuous_toFun
  inv_continuous := continuous_invFun

@[simp]
theorem coe_mk (e : Mâ‚ â‰ƒâ‚›â‚—[Ïƒâ‚â‚‚] Mâ‚‚) (a b) : â‡‘(ContinuousLinearEquiv.mk e a b) = e := rfl

theorem coe_apply (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) (b : Mâ‚) : (e : Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚) b = e b :=
  rfl

@[simp]
theorem coe_toLinearEquiv (f : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) : â‡‘f.toLinearEquiv = f :=
  rfl

@[simp, norm_cast]
theorem coe_coe (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) : â‡‘(e : Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚) = e :=
  rfl

theorem toLinearEquiv_injective :
    Function.Injective (toLinearEquiv : (Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) â†’ Mâ‚ â‰ƒâ‚›â‚—[Ïƒâ‚â‚‚] Mâ‚‚) := by
  rintro âŸ¨e, _, _âŸ© âŸ¨e', _, _âŸ© rfl
  rfl

@[ext]
theorem ext {f g : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚} (h : (f : Mâ‚ â†’ Mâ‚‚) = g) : f = g :=
  toLinearEquiv_injective <| LinearEquiv.ext <| congr_fun h

theorem coe_injective : Function.Injective ((â†‘) : (Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) â†’ Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚) :=
  fun _e _e' h => ext <| funext <| ContinuousLinearMap.ext_iff.1 h

@[simp, norm_cast]
theorem coe_inj {e e' : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚} : (e : Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚) = e' â†” e = e' :=
  coe_injective.eq_iff

/-- A continuous linear equivalence induces a homeomorphism. -/
def toHomeomorph (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) : Mâ‚ â‰ƒâ‚œ Mâ‚‚ :=
  { e with toEquiv := e.toLinearEquiv.toEquiv }

@[simp]
theorem coe_toHomeomorph (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) : â‡‘e.toHomeomorph = e :=
  rfl

theorem isOpenMap (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) : IsOpenMap e :=
  (ContinuousLinearEquiv.toHomeomorph e).isOpenMap

theorem image_closure (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) (s : Set Mâ‚) : e '' closure s = closure (e '' s) :=
  e.toHomeomorph.image_closure s

theorem preimage_closure (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) (s : Set Mâ‚‚) : e â»Â¹' closure s = closure (e â»Â¹' s) :=
  e.toHomeomorph.preimage_closure s

@[simp]
theorem isClosed_image (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) {s : Set Mâ‚} : IsClosed (e '' s) â†” IsClosed s :=
  e.toHomeomorph.isClosed_image

theorem map_nhds_eq (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) (x : Mâ‚) : map e (ğ“ x) = ğ“ (e x) :=
  e.toHomeomorph.map_nhds_eq x

-- Make some straightforward lemmas available to `simp`.
theorem map_zero (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) : e (0 : Mâ‚) = 0 :=
  (e : Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚).map_zero

theorem map_add (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) (x y : Mâ‚) : e (x + y) = e x + e y :=
  (e : Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚).map_add x y

@[simp]
theorem map_smulâ‚›â‚— (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) (c : Râ‚) (x : Mâ‚) : e (c â€¢ x) = Ïƒâ‚â‚‚ c â€¢ e x :=
  (e : Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚).map_smulâ‚›â‚— c x

theorem map_smul [Module Râ‚ Mâ‚‚] (e : Mâ‚ â‰ƒL[Râ‚] Mâ‚‚) (c : Râ‚) (x : Mâ‚) : e (c â€¢ x) = c â€¢ e x :=
  (e : Mâ‚ â†’L[Râ‚] Mâ‚‚).map_smul c x

theorem map_eq_zero_iff (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) {x : Mâ‚} : e x = 0 â†” x = 0 :=
  e.toLinearEquiv.map_eq_zero_iff

attribute [continuity]
  ContinuousLinearEquiv.continuous_toFun ContinuousLinearEquiv.continuous_invFun

@[continuity]
protected theorem continuous (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) : Continuous (e : Mâ‚ â†’ Mâ‚‚) :=
  e.continuous_toFun

protected theorem continuousOn (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) {s : Set Mâ‚} : ContinuousOn (e : Mâ‚ â†’ Mâ‚‚) s :=
  e.continuous.continuousOn

protected theorem continuousAt (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) {x : Mâ‚} : ContinuousAt (e : Mâ‚ â†’ Mâ‚‚) x :=
  e.continuous.continuousAt

protected theorem continuousWithinAt (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) {s : Set Mâ‚} {x : Mâ‚} :
    ContinuousWithinAt (e : Mâ‚ â†’ Mâ‚‚) s x :=
  e.continuous.continuousWithinAt

theorem comp_continuousOn_iff {Î± : Type*} [TopologicalSpace Î±] (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) {f : Î± â†’ Mâ‚}
    {s : Set Î±} : ContinuousOn (e âˆ˜ f) s â†” ContinuousOn f s :=
  e.toHomeomorph.comp_continuousOn_iff _ _

theorem comp_continuous_iff {Î± : Type*} [TopologicalSpace Î±] (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) {f : Î± â†’ Mâ‚} :
    Continuous (e âˆ˜ f) â†” Continuous f :=
  e.toHomeomorph.comp_continuous_iff

/-- An extensionality lemma for `R â‰ƒL[R] M`. -/
theorem extâ‚ [TopologicalSpace Râ‚] {f g : Râ‚ â‰ƒL[Râ‚] Mâ‚} (h : f 1 = g 1) : f = g :=
  ext <| funext fun x => mul_one x â–¸ by rw [â† smul_eq_mul, map_smul, h, map_smul]

section

variable (Râ‚ Mâ‚)

/-- The identity map as a continuous linear equivalence. -/
@[refl]
protected def refl : Mâ‚ â‰ƒL[Râ‚] Mâ‚ :=
  { LinearEquiv.refl Râ‚ Mâ‚ with
    continuous_toFun := continuous_id
    continuous_invFun := continuous_id }

@[simp]
theorem refl_apply (x : Mâ‚) :
    ContinuousLinearEquiv.refl Râ‚ Mâ‚ x = x := rfl

end

@[simp, norm_cast]
theorem coe_refl : â†‘(ContinuousLinearEquiv.refl Râ‚ Mâ‚) = ContinuousLinearMap.id Râ‚ Mâ‚ :=
  rfl

@[simp, norm_cast]
theorem coe_refl' : â‡‘(ContinuousLinearEquiv.refl Râ‚ Mâ‚) = id :=
  rfl

/-- The inverse of a continuous linear equivalence as a continuous linear equivalence -/
@[symm]
protected def symm (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) : Mâ‚‚ â‰ƒSL[Ïƒâ‚‚â‚] Mâ‚ :=
  { e.toLinearEquiv.symm with
    continuous_toFun := e.continuous_invFun
    continuous_invFun := e.continuous_toFun }

@[simp]
theorem toLinearEquiv_symm (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) : e.symm.toLinearEquiv = e.toLinearEquiv.symm :=
  rfl

@[deprecated (since := "2025-06-08")] alias symm_toLinearEquiv := toLinearEquiv_symm

@[simp]
theorem coe_symm_toLinearEquiv (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) : â‡‘e.toLinearEquiv.symm = e.symm :=
  rfl

@[simp]
theorem toHomeomorph_symm (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) : e.symm.toHomeomorph = e.toHomeomorph.symm :=
  rfl

@[deprecated "use instead `toHomeomorph_symm`, in the reverse direction" (since := "2025-06-08")]
theorem symm_toHomeomorph (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) : e.toHomeomorph.symm = e.symm.toHomeomorph :=
  rfl

@[simp]
theorem coe_symm_toHomeomorph (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) : â‡‘e.toHomeomorph.symm = e.symm :=
  rfl

/-- See Note [custom simps projection]. We need to specify this projection explicitly in this case,
  because it is a composition of multiple projections. -/
def Simps.apply (h : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) : Mâ‚ â†’ Mâ‚‚ :=
  h

/-- See Note [custom simps projection] -/
def Simps.symm_apply (h : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) : Mâ‚‚ â†’ Mâ‚ :=
  h.symm

initialize_simps_projections ContinuousLinearEquiv (toFun â†’ apply, invFun â†’ symm_apply)

theorem symm_map_nhds_eq (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) (x : Mâ‚) : map e.symm (ğ“ (e x)) = ğ“ x :=
  e.toHomeomorph.symm_map_nhds_eq x

/-- The composition of two continuous linear equivalences as a continuous linear equivalence. -/
@[trans]
protected def trans (eâ‚ : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) (eâ‚‚ : Mâ‚‚ â‰ƒSL[Ïƒâ‚‚â‚ƒ] Mâ‚ƒ) : Mâ‚ â‰ƒSL[Ïƒâ‚â‚ƒ] Mâ‚ƒ :=
  { eâ‚.toLinearEquiv.trans eâ‚‚.toLinearEquiv with
    continuous_toFun := eâ‚‚.continuous_toFun.comp eâ‚.continuous_toFun
    continuous_invFun := eâ‚.continuous_invFun.comp eâ‚‚.continuous_invFun }

@[simp]
theorem trans_toLinearEquiv (eâ‚ : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) (eâ‚‚ : Mâ‚‚ â‰ƒSL[Ïƒâ‚‚â‚ƒ] Mâ‚ƒ) :
    (eâ‚.trans eâ‚‚).toLinearEquiv = eâ‚.toLinearEquiv.trans eâ‚‚.toLinearEquiv := by
  ext
  rfl

/-- Product of two continuous linear equivalences. The map comes from `Equiv.prodCongr`. -/
def prodCongr [Module Râ‚ Mâ‚‚] [Module Râ‚ Mâ‚ƒ] [Module Râ‚ Mâ‚„] (e : Mâ‚ â‰ƒL[Râ‚] Mâ‚‚) (e' : Mâ‚ƒ â‰ƒL[Râ‚] Mâ‚„) :
    (Mâ‚ Ã— Mâ‚ƒ) â‰ƒL[Râ‚] Mâ‚‚ Ã— Mâ‚„ :=
  { e.toLinearEquiv.prodCongr e'.toLinearEquiv with
    continuous_toFun := e.continuous_toFun.prodMap e'.continuous_toFun
    continuous_invFun := e.continuous_invFun.prodMap e'.continuous_invFun }

@[deprecated (since := "2025-06-06")] alias prod := prodCongr

@[simp, norm_cast]
theorem prodCongr_apply [Module Râ‚ Mâ‚‚] [Module Râ‚ Mâ‚ƒ] [Module Râ‚ Mâ‚„] (e : Mâ‚ â‰ƒL[Râ‚] Mâ‚‚)
    (e' : Mâ‚ƒ â‰ƒL[Râ‚] Mâ‚„) (x) : e.prodCongr e' x = (e x.1, e' x.2) :=
  rfl

@[deprecated (since := "2025-06-06")] alias prod_apply := prodCongr_apply

@[simp, norm_cast]
theorem coe_prodCongr [Module Râ‚ Mâ‚‚] [Module Râ‚ Mâ‚ƒ] [Module Râ‚ Mâ‚„] (e : Mâ‚ â‰ƒL[Râ‚] Mâ‚‚)
    (e' : Mâ‚ƒ â‰ƒL[Râ‚] Mâ‚„) :
    (e.prodCongr e' : Mâ‚ Ã— Mâ‚ƒ â†’L[Râ‚] Mâ‚‚ Ã— Mâ‚„) = (e : Mâ‚ â†’L[Râ‚] Mâ‚‚).prodMap (e' : Mâ‚ƒ â†’L[Râ‚] Mâ‚„) :=
  rfl

@[deprecated (since := "2025-06-06")] alias coe_prod := coe_prodCongr

theorem prodCongr_symm [Module Râ‚ Mâ‚‚] [Module Râ‚ Mâ‚ƒ] [Module Râ‚ Mâ‚„] (e : Mâ‚ â‰ƒL[Râ‚] Mâ‚‚)
    (e' : Mâ‚ƒ â‰ƒL[Râ‚] Mâ‚„) : (e.prodCongr e').symm = e.symm.prodCongr e'.symm :=
  rfl

@[deprecated (since := "2025-06-06")] alias prod_symm := prodCongr_symm

variable (Râ‚ Mâ‚ Mâ‚‚)

/-- Product of modules is commutative up to continuous linear isomorphism. -/
@[simps! apply toLinearEquiv]
def prodComm [Module Râ‚ Mâ‚‚] : (Mâ‚ Ã— Mâ‚‚) â‰ƒL[Râ‚] Mâ‚‚ Ã— Mâ‚ :=
  { LinearEquiv.prodComm Râ‚ Mâ‚ Mâ‚‚ with
    continuous_toFun := continuous_swap
    continuous_invFun := continuous_swap }

@[simp] lemma prodComm_symm [Module Râ‚ Mâ‚‚] : (prodComm Râ‚ Mâ‚ Mâ‚‚).symm = prodComm Râ‚ Mâ‚‚ Mâ‚ := rfl

section prodAssoc

variable (R Mâ‚ Mâ‚‚ Mâ‚ƒ : Type*) [Semiring R]
  [AddCommMonoid Mâ‚] [AddCommMonoid Mâ‚‚] [AddCommMonoid Mâ‚ƒ] [Module R Mâ‚] [Module R Mâ‚‚] [Module R Mâ‚ƒ]
  [TopologicalSpace Mâ‚] [TopologicalSpace Mâ‚‚] [TopologicalSpace Mâ‚ƒ]

/-- The product of topological modules is associative up to continuous linear isomorphism.
This is `LinearEquiv.prodAssoc` prodAssoc as a continuous linear equivalence. -/
def prodAssoc : ((Mâ‚ Ã— Mâ‚‚) Ã— Mâ‚ƒ) â‰ƒL[R] Mâ‚ Ã— Mâ‚‚ Ã— Mâ‚ƒ where
  toLinearEquiv := LinearEquiv.prodAssoc R Mâ‚ Mâ‚‚ Mâ‚ƒ
  continuous_toFun := (continuous_fst.comp continuous_fst).prodMk
    ((continuous_snd.comp continuous_fst).prodMk continuous_snd)
  continuous_invFun := (continuous_fst.prodMk (continuous_fst.comp continuous_snd)).prodMk
    (continuous_snd.comp continuous_snd)

@[simp]
lemma prodAssoc_toLinearEquiv :
    (prodAssoc R Mâ‚ Mâ‚‚ Mâ‚ƒ).toLinearEquiv = LinearEquiv.prodAssoc R Mâ‚ Mâ‚‚ Mâ‚ƒ := rfl

@[simp]
lemma coe_prodAssoc :
    (prodAssoc R Mâ‚ Mâ‚‚ Mâ‚ƒ : (Mâ‚ Ã— Mâ‚‚) Ã— Mâ‚ƒ â†’ Mâ‚ Ã— Mâ‚‚ Ã— Mâ‚ƒ) = Equiv.prodAssoc Mâ‚ Mâ‚‚ Mâ‚ƒ := rfl

@[simp]
lemma prodAssoc_apply (pâ‚ : Mâ‚) (pâ‚‚ : Mâ‚‚) (pâ‚ƒ : Mâ‚ƒ) :
    prodAssoc R Mâ‚ Mâ‚‚ Mâ‚ƒ ((pâ‚, pâ‚‚), pâ‚ƒ) = (pâ‚, (pâ‚‚, pâ‚ƒ)) := rfl

@[simp]
lemma prodAssoc_symm_apply (pâ‚ : Mâ‚) (pâ‚‚ : Mâ‚‚) (pâ‚ƒ : Mâ‚ƒ) :
    (prodAssoc R Mâ‚ Mâ‚‚ Mâ‚ƒ).symm (pâ‚, (pâ‚‚, pâ‚ƒ)) = ((pâ‚, pâ‚‚), pâ‚ƒ) := rfl

end prodAssoc

section prodUnique

variable (R M N : Type*) [Semiring R]
  [TopologicalSpace M] [AddCommMonoid M] [TopologicalSpace N] [AddCommMonoid N]
  [Unique N] [Module R M] [Module R N]

/-- The natural equivalence `M Ã— N â‰ƒL[R] M` for any `Unique` type `N`.
This is `Equiv.prodUnique` as a continuous linear equivalence. -/
def prodUnique : (M Ã— N) â‰ƒL[R] M where
  toLinearEquiv := LinearEquiv.prodUnique
  continuous_toFun := by
    change Continuous (Equiv.prodUnique M N)
    dsimp; fun_prop
  continuous_invFun := by
    change Continuous fun x â†¦ (x, default)
    fun_prop

@[simp]
lemma coe_prodUnique : (prodUnique R M N).toEquiv = Equiv.prodUnique M N := rfl

@[simp]
lemma prodUnique_apply (x : M Ã— N) : prodUnique R M N x = x.1 := rfl

@[simp]
lemma prodUnique_symm_apply (x : M) : (prodUnique R M N).symm x = (x, default) := rfl

/-- The natural equivalence `N Ã— M â‰ƒL[R] M` for any `Unique` type `N`.
This is `Equiv.uniqueProd` as a continuous linear equivalence. -/
def uniqueProd : (N Ã— M) â‰ƒL[R] M where
  toLinearEquiv := LinearEquiv.uniqueProd
  continuous_toFun := by
    change Continuous (Equiv.uniqueProd M N)
    dsimp; fun_prop
  continuous_invFun := by
    change Continuous fun x â†¦ (default, x)
    fun_prop

@[simp]
lemma coe_uniqueProd : (uniqueProd R M N).toEquiv = Equiv.uniqueProd M N := rfl

@[simp]
lemma uniqueProd_apply (x : N Ã— M) : uniqueProd R M N x = x.2 := rfl

@[simp]
lemma uniqueProd_symm_apply (x : M) : (uniqueProd R M N).symm x = (default, x) := rfl

end prodUnique

variable {Râ‚ Mâ‚ Mâ‚‚}

protected theorem bijective (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) : Function.Bijective e :=
  e.toLinearEquiv.toEquiv.bijective

protected theorem injective (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) : Function.Injective e :=
  e.toLinearEquiv.toEquiv.injective

protected theorem surjective (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) : Function.Surjective e :=
  e.toLinearEquiv.toEquiv.surjective

@[simp]
theorem trans_apply (eâ‚ : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) (eâ‚‚ : Mâ‚‚ â‰ƒSL[Ïƒâ‚‚â‚ƒ] Mâ‚ƒ) (c : Mâ‚) :
    (eâ‚.trans eâ‚‚) c = eâ‚‚ (eâ‚ c) :=
  rfl

@[simp]
theorem apply_symm_apply (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) (c : Mâ‚‚) : e (e.symm c) = c :=
  e.1.right_inv c

@[simp]
theorem symm_apply_apply (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) (b : Mâ‚) : e.symm (e b) = b :=
  e.1.left_inv b

@[simp]
theorem symm_trans_apply (eâ‚ : Mâ‚‚ â‰ƒSL[Ïƒâ‚‚â‚] Mâ‚) (eâ‚‚ : Mâ‚ƒ â‰ƒSL[Ïƒâ‚ƒâ‚‚] Mâ‚‚) (c : Mâ‚) :
    (eâ‚‚.trans eâ‚).symm c = eâ‚‚.symm (eâ‚.symm c) :=
  rfl

@[simp]
theorem symm_image_image (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) (s : Set Mâ‚) : e.symm '' (e '' s) = s :=
  e.toLinearEquiv.toEquiv.symm_image_image s

@[simp]
theorem image_symm_image (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) (s : Set Mâ‚‚) : e '' (e.symm '' s) = s :=
  e.symm.symm_image_image s

@[simp, norm_cast]
theorem comp_coe (f : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) (f' : Mâ‚‚ â‰ƒSL[Ïƒâ‚‚â‚ƒ] Mâ‚ƒ) :
    (f' : Mâ‚‚ â†’SL[Ïƒâ‚‚â‚ƒ] Mâ‚ƒ).comp (f : Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚) = (f.trans f' : Mâ‚ â†’SL[Ïƒâ‚â‚ƒ] Mâ‚ƒ) :=
  rfl

-- The priority should be higher than `comp_coe`.
@[simp high]
theorem coe_comp_coe_symm (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) :
    (e : Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚).comp (e.symm : Mâ‚‚ â†’SL[Ïƒâ‚‚â‚] Mâ‚) = ContinuousLinearMap.id Râ‚‚ Mâ‚‚ :=
  ContinuousLinearMap.ext e.apply_symm_apply

-- The priority should be higher than `comp_coe`.
@[simp high]
theorem coe_symm_comp_coe (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) :
    (e.symm : Mâ‚‚ â†’SL[Ïƒâ‚‚â‚] Mâ‚).comp (e : Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚) = ContinuousLinearMap.id Râ‚ Mâ‚ :=
  ContinuousLinearMap.ext e.symm_apply_apply

@[simp]
theorem symm_comp_self (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) : (e.symm : Mâ‚‚ â†’ Mâ‚) âˆ˜ (e : Mâ‚ â†’ Mâ‚‚) = id := by
  ext x
  exact symm_apply_apply e x

@[simp]
theorem self_comp_symm (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) : (e : Mâ‚ â†’ Mâ‚‚) âˆ˜ (e.symm : Mâ‚‚ â†’ Mâ‚) = id := by
  ext x
  exact apply_symm_apply e x

@[simp]
theorem symm_symm (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) : e.symm.symm = e := rfl

theorem symm_bijective : Function.Bijective (ContinuousLinearEquiv.symm : (Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) â†’ _) :=
  Function.bijective_iff_has_inverse.mpr âŸ¨_, symm_symm, symm_symmâŸ©

@[simp]
theorem refl_symm : (ContinuousLinearEquiv.refl Râ‚ Mâ‚).symm = ContinuousLinearEquiv.refl Râ‚ Mâ‚ :=
  rfl

theorem symm_symm_apply (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) (x : Mâ‚) : e.symm.symm x = e x :=
  rfl

theorem symm_apply_eq (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) {x y} : e.symm x = y â†” x = e y :=
  e.toLinearEquiv.symm_apply_eq

theorem eq_symm_apply (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) {x y} : y = e.symm x â†” e y = x :=
  e.toLinearEquiv.eq_symm_apply

protected theorem image_eq_preimage (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) (s : Set Mâ‚) : e '' s = e.symm â»Â¹' s :=
  e.toLinearEquiv.toEquiv.image_eq_preimage s

protected theorem image_symm_eq_preimage (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) (s : Set Mâ‚‚) :
    e.symm '' s = e â»Â¹' s := by rw [e.symm.image_eq_preimage, e.symm_symm]

@[simp]
protected theorem symm_preimage_preimage (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) (s : Set Mâ‚‚) :
    e.symm â»Â¹' (e â»Â¹' s) = s :=
  e.toLinearEquiv.toEquiv.symm_preimage_preimage s

@[simp]
protected theorem preimage_symm_preimage (e : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) (s : Set Mâ‚) :
    e â»Â¹' (e.symm â»Â¹' s) = s :=
  e.symm.symm_preimage_preimage s

lemma isUniformEmbedding {Eâ‚ Eâ‚‚ : Type*} [UniformSpace Eâ‚] [UniformSpace Eâ‚‚]
    [AddCommGroup Eâ‚] [AddCommGroup Eâ‚‚] [Module Râ‚ Eâ‚] [Module Râ‚‚ Eâ‚‚] [IsUniformAddGroup Eâ‚]
    [IsUniformAddGroup Eâ‚‚] (e : Eâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Eâ‚‚) : IsUniformEmbedding e :=
  e.toLinearEquiv.toEquiv.isUniformEmbedding e.toContinuousLinearMap.uniformContinuous
    e.symm.toContinuousLinearMap.uniformContinuous

protected theorem _root_.LinearEquiv.isUniformEmbedding {Eâ‚ Eâ‚‚ : Type*} [UniformSpace Eâ‚]
    [UniformSpace Eâ‚‚] [AddCommGroup Eâ‚] [AddCommGroup Eâ‚‚] [Module Râ‚ Eâ‚] [Module Râ‚‚ Eâ‚‚]
    [IsUniformAddGroup Eâ‚] [IsUniformAddGroup Eâ‚‚] (e : Eâ‚ â‰ƒâ‚›â‚—[Ïƒâ‚â‚‚] Eâ‚‚)
    (hâ‚ : Continuous e) (hâ‚‚ : Continuous e.symm) : IsUniformEmbedding e :=
  ContinuousLinearEquiv.isUniformEmbedding
    ({ e with
        continuous_toFun := hâ‚
        continuous_invFun := hâ‚‚ } :
      Eâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Eâ‚‚)

/-- Create a `ContinuousLinearEquiv` from two `ContinuousLinearMap`s that are
inverse of each other. See also `equivOfInverse'`. -/
def equivOfInverse (fâ‚ : Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚) (fâ‚‚ : Mâ‚‚ â†’SL[Ïƒâ‚‚â‚] Mâ‚) (hâ‚ : Function.LeftInverse fâ‚‚ fâ‚)
    (hâ‚‚ : Function.RightInverse fâ‚‚ fâ‚) : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚ :=
  { fâ‚ with
    continuous_toFun := fâ‚.continuous
    invFun := fâ‚‚
    continuous_invFun := fâ‚‚.continuous
    left_inv := hâ‚
    right_inv := hâ‚‚ }

@[simp]
theorem equivOfInverse_apply (fâ‚ : Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚) (fâ‚‚ hâ‚ hâ‚‚ x) :
    equivOfInverse fâ‚ fâ‚‚ hâ‚ hâ‚‚ x = fâ‚ x :=
  rfl

@[simp]
theorem symm_equivOfInverse (fâ‚ : Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚) (fâ‚‚ hâ‚ hâ‚‚) :
    (equivOfInverse fâ‚ fâ‚‚ hâ‚ hâ‚‚).symm = equivOfInverse fâ‚‚ fâ‚ hâ‚‚ hâ‚ :=
  rfl

/-- Create a `ContinuousLinearEquiv` from two `ContinuousLinearMap`s that are
inverse of each other, in the `ContinuousLinearMap.comp` sense. See also `equivOfInverse`. -/
def equivOfInverse' (fâ‚ : Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚) (fâ‚‚ : Mâ‚‚ â†’SL[Ïƒâ‚‚â‚] Mâ‚)
    (hâ‚ : fâ‚.comp fâ‚‚ = .id Râ‚‚ Mâ‚‚) (hâ‚‚ : fâ‚‚.comp fâ‚ = .id Râ‚ Mâ‚) : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚ :=
  equivOfInverse fâ‚ fâ‚‚
    (fun x â†¦ by simpa using congr($(hâ‚‚) x)) (fun x â†¦ by simpa using congr($(hâ‚) x))

@[simp]
theorem equivOfInverse'_apply (fâ‚ : Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚) (fâ‚‚ hâ‚ hâ‚‚ x) :
    equivOfInverse' fâ‚ fâ‚‚ hâ‚ hâ‚‚ x = fâ‚ x :=
  rfl

/-- The inverse of `equivOfInverse'` is obtained by swapping the order of its parameters. -/
@[simp]
theorem symm_equivOfInverse' (fâ‚ : Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚) (fâ‚‚ hâ‚ hâ‚‚) :
    (equivOfInverse' fâ‚ fâ‚‚ hâ‚ hâ‚‚).symm = equivOfInverse' fâ‚‚ fâ‚ hâ‚‚ hâ‚ :=
  rfl

variable (Mâ‚)

/-- The continuous linear equivalences from `M` to itself form a group under composition. -/
instance automorphismGroup : Group (Mâ‚ â‰ƒL[Râ‚] Mâ‚) where
  mul f g := g.trans f
  one := ContinuousLinearEquiv.refl Râ‚ Mâ‚
  inv f := f.symm
  mul_assoc f g h := by
    ext
    rfl
  mul_one f := by
    ext
    rfl
  one_mul f := by
    ext
    rfl
  inv_mul_cancel f := by
    ext x
    exact f.left_inv x

variable {Mâ‚} {Râ‚„ : Type*} [Semiring Râ‚„] [Module Râ‚„ Mâ‚„] {Ïƒâ‚ƒâ‚„ : Râ‚ƒ â†’+* Râ‚„} {Ïƒâ‚„â‚ƒ : Râ‚„ â†’+* Râ‚ƒ}
  [RingHomInvPair Ïƒâ‚ƒâ‚„ Ïƒâ‚„â‚ƒ] [RingHomInvPair Ïƒâ‚„â‚ƒ Ïƒâ‚ƒâ‚„] {Ïƒâ‚‚â‚„ : Râ‚‚ â†’+* Râ‚„} {Ïƒâ‚â‚„ : Râ‚ â†’+* Râ‚„}
  [RingHomCompTriple Ïƒâ‚‚â‚ Ïƒâ‚â‚„ Ïƒâ‚‚â‚„] [RingHomCompTriple Ïƒâ‚‚â‚„ Ïƒâ‚„â‚ƒ Ïƒâ‚‚â‚ƒ] [RingHomCompTriple Ïƒâ‚â‚ƒ Ïƒâ‚ƒâ‚„ Ïƒâ‚â‚„]

/-- The continuous linear equivalence between `ULift Mâ‚` and `Mâ‚`.

This is a continuous version of `ULift.moduleEquiv`. -/
def ulift : ULift Mâ‚ â‰ƒL[Râ‚] Mâ‚ :=
  { ULift.moduleEquiv with
    continuous_toFun := continuous_uliftDown
    continuous_invFun := continuous_uliftUp }

/-- A pair of continuous (semi)linear equivalences generates an equivalence between the spaces of
continuous linear maps. See also `ContinuousLinearEquiv.arrowCongr`. -/
@[simps]
def arrowCongrEquiv (eâ‚â‚‚ : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) (eâ‚„â‚ƒ : Mâ‚„ â‰ƒSL[Ïƒâ‚„â‚ƒ] Mâ‚ƒ) :
    (Mâ‚ â†’SL[Ïƒâ‚â‚„] Mâ‚„) â‰ƒ (Mâ‚‚ â†’SL[Ïƒâ‚‚â‚ƒ] Mâ‚ƒ) where
  toFun f := (eâ‚„â‚ƒ : Mâ‚„ â†’SL[Ïƒâ‚„â‚ƒ] Mâ‚ƒ).comp (f.comp (eâ‚â‚‚.symm : Mâ‚‚ â†’SL[Ïƒâ‚‚â‚] Mâ‚))
  invFun f := (eâ‚„â‚ƒ.symm : Mâ‚ƒ â†’SL[Ïƒâ‚ƒâ‚„] Mâ‚„).comp (f.comp (eâ‚â‚‚ : Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚))
  left_inv f :=
    ContinuousLinearMap.ext fun x => by
      simp only [ContinuousLinearMap.comp_apply, symm_apply_apply, coe_coe]
  right_inv f :=
    ContinuousLinearMap.ext fun x => by
      simp only [ContinuousLinearMap.comp_apply, apply_symm_apply, coe_coe]

section Pi

/-- Combine a family of linear equivalences into a linear equivalence of `pi`-types.
This is `Equiv.piCongrLeft` as a `ContinuousLinearEquiv`.
-/
def piCongrLeft (R : Type*) [Semiring R] {Î¹ Î¹' : Type*}
    (Ï† : Î¹ â†’ Type*) [âˆ€ i, AddCommMonoid (Ï† i)] [âˆ€ i, Module R (Ï† i)]
    [âˆ€ i, TopologicalSpace (Ï† i)]
    (e : Î¹' â‰ƒ Î¹) : ((i' : Î¹') â†’ Ï† (e i')) â‰ƒL[R] (i : Î¹) â†’ Ï† i where
  __ := Homeomorph.piCongrLeft e
  __ := LinearEquiv.piCongrLeft R Ï† e

/-- The product over `S âŠ• T` of a family of topological modules
is isomorphic (topologically and algebraically) to the product of
(the product over `S`) and (the product over `T`).

This is `Equiv.sumPiEquivProdPi` as a `ContinuousLinearEquiv`.
-/
def sumPiEquivProdPi (R : Type*) [Semiring R] (S T : Type*)
    (A : S âŠ• T â†’ Type*) [âˆ€ st, AddCommMonoid (A st)] [âˆ€ st, Module R (A st)]
    [âˆ€ st, TopologicalSpace (A st)] :
    ((st : S âŠ• T) â†’ A st) â‰ƒL[R] ((s : S) â†’ A (Sum.inl s)) Ã— ((t : T) â†’ A (Sum.inr t)) where
  __ := LinearEquiv.sumPiEquivProdPi R S T A
  __ := Homeomorph.sumPiEquivProdPi S T A

/-- The product `Î  t : Î±, f t` of a family of topological modules is isomorphic
(both topologically and algebraically) to the space `f â¬` when `Î±` only contains `â¬`.

This is `Equiv.piUnique` as a `ContinuousLinearEquiv`.
-/
@[simps! -fullyApplied]
def piUnique {Î± : Type*} [Unique Î±] (R : Type*) [Semiring R] (f : Î± â†’ Type*)
    [âˆ€ x, AddCommMonoid (f x)] [âˆ€ x, Module R (f x)] [âˆ€ x, TopologicalSpace (f x)] :
    (Î  t, f t) â‰ƒL[R] f default where
  __ := LinearEquiv.piUnique R f
  __ := Homeomorph.piUnique f

end Pi

section piCongrRight

variable {Î¹ : Type*} {M : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (M i)] [âˆ€ i, AddCommMonoid (M i)]
  [âˆ€ i, Module Râ‚ (M i)] {N : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (N i)] [âˆ€ i, AddCommMonoid (N i)]
  [âˆ€ i, Module Râ‚ (N i)] (f : (i : Î¹) â†’ M i â‰ƒL[Râ‚] N i)

/-- Combine a family of continuous linear equivalences into a continuous linear equivalence of
pi-types. -/
def piCongrRight : ((i : Î¹) â†’ M i) â‰ƒL[Râ‚] (i : Î¹) â†’ N i :=
  { LinearEquiv.piCongrRight fun i â†¦ f i with
    continuous_toFun := by
      exact continuous_pi fun i â†¦ (f i).continuous_toFun.comp (continuous_apply i)
    continuous_invFun := by
      exact continuous_pi fun i => (f i).continuous_invFun.comp (continuous_apply i) }

@[simp]
theorem piCongrRight_apply (m : (i : Î¹) â†’ M i) (i : Î¹) :
    piCongrRight f m i = (f i) (m i) := rfl

@[simp]
theorem piCongrRight_symm_apply (n : (i : Î¹) â†’ N i) (i : Î¹) :
    (piCongrRight f).symm n i = (f i).symm (n i) := rfl

end piCongrRight

section DistribMulAction

variable {G : Type*} [Group G] [DistribMulAction G Mâ‚] [ContinuousConstSMul G Mâ‚]
  [SMulCommClass G Râ‚ Mâ‚]

/-- Scalar multiplication by a group element as a continuous linear equivalence. -/
@[simps! apply_toLinearEquiv apply_apply]
def smulLeft : G â†’* Mâ‚ â‰ƒL[Râ‚] Mâ‚ where
  toFun g := âŸ¨DistribMulAction.toModuleAut _ _ g, continuous_const_smul _, continuous_const_smul _âŸ©
  map_mul' _ _ := toLinearEquiv_injective <| map_mul (DistribMulAction.toModuleAut _ _) _ _
  map_one' := toLinearEquiv_injective <| map_one <| DistribMulAction.toModuleAut _ _

end DistribMulAction

end AddCommMonoid

section Aut

/-!
### Automorphisms as continuous linear equivalences and as units of the ring of endomorphisms

The next theorems cover the identification between `M â‰ƒL[R] M`and the group of units of the ring
`M â†’L[R] M`.
-/

variable {R M : Type*} [Semiring R] [AddCommMonoid M] [Module R M] [TopologicalSpace M]

/-- An invertible continuous linear map `f` determines a continuous equivalence from `M` to itself.
-/
def ofUnit (f : (M â†’L[R] M)Ë£) : M â‰ƒL[R] M where
  toLinearEquiv :=
    { toFun := f.val
      map_add' := by simp
      map_smul' := by simp
      invFun := f.inv
      left_inv := fun x =>
        show (f.inv * f.val) x = x by
          rw [f.inv_val]
          simp
      right_inv := fun x =>
        show (f.val * f.inv) x = x by
          rw [f.val_inv]
          simp }
  continuous_toFun := f.val.continuous
  continuous_invFun := f.inv.continuous

/-- A continuous equivalence from `M` to itself determines an invertible continuous linear map. -/
def toUnit (f : M â‰ƒL[R] M) : (M â†’L[R] M)Ë£ where
  val := f
  inv := f.symm
  val_inv := by
    ext
    simp
  inv_val := by
    ext
    simp

variable (R M)

/-- The units of the algebra of continuous `R`-linear endomorphisms of `M` is multiplicatively
equivalent to the type of continuous linear equivalences between `M` and itself. -/
def unitsEquiv : (M â†’L[R] M)Ë£ â‰ƒ* M â‰ƒL[R] M where
  toFun := ofUnit
  invFun := toUnit
  map_mul' x y := by
    ext
    rfl

@[simp]
theorem unitsEquiv_apply (f : (M â†’L[R] M)Ë£) (x : M) : unitsEquiv R M f x = (f : M â†’L[R] M) x :=
  rfl

end Aut

section AutRing

/-!
### Units of a ring as linear automorphisms
-/

variable (R : Type*) [Semiring R] [TopologicalSpace R] [ContinuousMul R]

/-- Continuous linear equivalences `R â‰ƒL[R] R` are enumerated by `RË£`. -/
def unitsEquivAut : RË£ â‰ƒ R â‰ƒL[R] R where
  toFun u :=
    equivOfInverse (ContinuousLinearMap.smulRight (1 : R â†’L[R] R) â†‘u)
      (ContinuousLinearMap.smulRight (1 : R â†’L[R] R) â†‘uâ»Â¹) (fun x => by simp) fun x => by simp
  invFun e :=
    âŸ¨e 1, e.symm 1, by rw [â† smul_eq_mul, â† map_smul, smul_eq_mul, mul_one, symm_apply_apply], by
      rw [â† smul_eq_mul, â† map_smul, smul_eq_mul, mul_one, apply_symm_apply]âŸ©
  left_inv u := Units.ext <| by simp
  right_inv e := extâ‚ <| by simp

variable {R}

@[simp]
theorem unitsEquivAut_apply (u : RË£) (x : R) : unitsEquivAut R u x = x * u :=
  rfl

@[simp]
theorem unitsEquivAut_apply_symm (u : RË£) (x : R) : (unitsEquivAut R u).symm x = x * â†‘uâ»Â¹ :=
  rfl

@[simp]
theorem unitsEquivAut_symm_apply (e : R â‰ƒL[R] R) : â†‘((unitsEquivAut R).symm e) = e 1 :=
  rfl

end AutRing

section Pi

variable (Î¹ R M : Type*) [Unique Î¹] [Semiring R] [AddCommMonoid M] [Module R M]
  [TopologicalSpace M]

/-- If `Î¹` has a unique element, then `Î¹ â†’ M` is continuously linear equivalent to `M`. -/
def funUnique : (Î¹ â†’ M) â‰ƒL[R] M :=
  { Homeomorph.funUnique Î¹ M with toLinearEquiv := LinearEquiv.funUnique Î¹ R M }

variable {Î¹ R M}

@[simp]
theorem coe_funUnique : â‡‘(funUnique Î¹ R M) = Function.eval default :=
  rfl

@[simp]
theorem coe_funUnique_symm : â‡‘(funUnique Î¹ R M).symm = Function.const Î¹ :=
  rfl

variable (R M)

/-- Continuous linear equivalence between dependent functions `(i : Fin 2) â†’ M i` and `M 0 Ã— M 1`.
-/
@[simps! -fullyApplied apply symm_apply]
def piFinTwo (M : Fin 2 â†’ Type*) [âˆ€ i, AddCommMonoid (M i)] [âˆ€ i, Module R (M i)]
    [âˆ€ i, TopologicalSpace (M i)] : ((i : _) â†’ M i) â‰ƒL[R] M 0 Ã— M 1 :=
  { Homeomorph.piFinTwo M with toLinearEquiv := LinearEquiv.piFinTwo R M }

/-- Continuous linear equivalence between vectors in `MÂ² = Fin 2 â†’ M` and `M Ã— M`. -/
@[simps! -fullyApplied apply symm_apply]
def finTwoArrow : (Fin 2 â†’ M) â‰ƒL[R] M Ã— M :=
  { piFinTwo R fun _ => M with toLinearEquiv := LinearEquiv.finTwoArrow R M }

section
variable {n : â„•} {R : Type*} {M : Fin n.succ â†’ Type*} {N : Type*}
variable [Semiring R]
variable [âˆ€ i, AddCommMonoid (M i)] [âˆ€ i, Module R (M i)] [âˆ€ i, TopologicalSpace (M i)]

variable (R M) in
/-- `Fin.consEquiv` as a continuous linear equivalence. -/
@[simps!]
def _root_.Fin.consEquivL : (M 0 Ã— Î  i, M (Fin.succ i)) â‰ƒL[R] (Î  i, M i) where
  __ := Fin.consLinearEquiv R M
  continuous_toFun := continuous_id.fst.finCons continuous_id.snd
  continuous_invFun := .prodMk (continuous_apply 0) (by continuity)

/-- `Fin.cons` in the codomain of continuous linear maps. -/
abbrev _root_.ContinuousLinearMap.finCons
    [AddCommMonoid N] [Module R N] [TopologicalSpace N]
    (f : N â†’L[R] M 0) (fs : N â†’L[R] Î  i, M (Fin.succ i)) :
    N â†’L[R] Î  i, M i :=
  Fin.consEquivL R M âˆ˜L f.prod fs

end

end Pi

section AddCommGroup

variable {R : Type*} [Semiring R] {M : Type*} [TopologicalSpace M] [AddCommGroup M] {Mâ‚‚ : Type*}
  [TopologicalSpace Mâ‚‚] [AddCommGroup Mâ‚‚] {Mâ‚ƒ : Type*} [TopologicalSpace Mâ‚ƒ] [AddCommGroup Mâ‚ƒ]
  {Mâ‚„ : Type*} [TopologicalSpace Mâ‚„] [AddCommGroup Mâ‚„] [Module R M] [Module R Mâ‚‚] [Module R Mâ‚ƒ]
  [Module R Mâ‚„]

variable [IsTopologicalAddGroup Mâ‚„]

/-- Equivalence given by a block lower diagonal matrix. `e` and `e'` are diagonal square blocks,
  and `f` is a rectangular block below the diagonal. -/
def skewProd (e : M â‰ƒL[R] Mâ‚‚) (e' : Mâ‚ƒ â‰ƒL[R] Mâ‚„) (f : M â†’L[R] Mâ‚„) : (M Ã— Mâ‚ƒ) â‰ƒL[R] Mâ‚‚ Ã— Mâ‚„ :=
  { e.toLinearEquiv.skewProd e'.toLinearEquiv â†‘f with
    continuous_toFun :=
      (e.continuous_toFun.comp continuous_fst).prodMk
        ((e'.continuous_toFun.comp continuous_snd).add <| f.continuous.comp continuous_fst)
    continuous_invFun :=
      (e.continuous_invFun.comp continuous_fst).prodMk
        (e'.continuous_invFun.comp <|
          continuous_snd.sub <| f.continuous.comp <| e.continuous_invFun.comp continuous_fst) }

@[simp]
theorem skewProd_apply (e : M â‰ƒL[R] Mâ‚‚) (e' : Mâ‚ƒ â‰ƒL[R] Mâ‚„) (f : M â†’L[R] Mâ‚„) (x) :
    e.skewProd e' f x = (e x.1, e' x.2 + f x.1) :=
  rfl

@[simp]
theorem skewProd_symm_apply (e : M â‰ƒL[R] Mâ‚‚) (e' : Mâ‚ƒ â‰ƒL[R] Mâ‚„) (f : M â†’L[R] Mâ‚„) (x) :
    (e.skewProd e' f).symm x = (e.symm x.1, e'.symm (x.2 - f (e.symm x.1))) :=
  rfl

variable (R) in
/-- The negation map as a continuous linear equivalence. -/
def neg [ContinuousNeg M] :
    M â‰ƒL[R] M :=
  { LinearEquiv.neg R with
    continuous_toFun := continuous_neg
    continuous_invFun := continuous_neg }

@[simp]
theorem coe_neg [ContinuousNeg M] :
    (neg R : M â†’ M) = -id := rfl

@[simp]
theorem neg_apply [ContinuousNeg M] (x : M) :
    neg R x = -x := by simp

@[simp]
theorem symm_neg [ContinuousNeg M] :
    (neg R : M â‰ƒL[R] M).symm = neg R := rfl

end AddCommGroup

section Ring

variable {R : Type*} [Ring R] {Râ‚‚ : Type*} [Ring Râ‚‚] {M : Type*} [TopologicalSpace M]
  [AddCommGroup M] [Module R M] {Mâ‚‚ : Type*} [TopologicalSpace Mâ‚‚] [AddCommGroup Mâ‚‚] [Module Râ‚‚ Mâ‚‚]

variable {Ïƒâ‚â‚‚ : R â†’+* Râ‚‚} {Ïƒâ‚‚â‚ : Râ‚‚ â†’+* R} [RingHomInvPair Ïƒâ‚â‚‚ Ïƒâ‚‚â‚] [RingHomInvPair Ïƒâ‚‚â‚ Ïƒâ‚â‚‚]

theorem map_sub (e : M â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) (x y : M) : e (x - y) = e x - e y :=
  (e : M â†’SL[Ïƒâ‚â‚‚] Mâ‚‚).map_sub x y

theorem map_neg (e : M â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚) (x : M) : e (-x) = -e x :=
  (e : M â†’SL[Ïƒâ‚â‚‚] Mâ‚‚).map_neg x

variable [Module R Mâ‚‚] [IsTopologicalAddGroup M]

/-- A pair of continuous linear maps such that `fâ‚ âˆ˜ fâ‚‚ = id` generates a continuous
linear equivalence `e` between `M` and `Mâ‚‚ Ã— fâ‚.ker` such that `(e x).2 = x` for `x âˆˆ fâ‚.ker`,
`(e x).1 = fâ‚ x`, and `(e (fâ‚‚ y)).2 = 0`. The map is given by `e x = (fâ‚ x, x - fâ‚‚ (fâ‚ x))`. -/
def equivOfRightInverse (fâ‚ : M â†’L[R] Mâ‚‚) (fâ‚‚ : Mâ‚‚ â†’L[R] M) (h : Function.RightInverse fâ‚‚ fâ‚) :
    M â‰ƒL[R] Mâ‚‚ Ã— ker fâ‚ :=
  equivOfInverse (fâ‚.prod (fâ‚.projKerOfRightInverse fâ‚‚ h)) (fâ‚‚.coprod (ker fâ‚).subtypeL)
    (fun x => by simp) fun âŸ¨x, yâŸ© => by simp [h x]

@[simp]
theorem fst_equivOfRightInverse (fâ‚ : M â†’L[R] Mâ‚‚) (fâ‚‚ : Mâ‚‚ â†’L[R] M)
    (h : Function.RightInverse fâ‚‚ fâ‚) (x : M) : (equivOfRightInverse fâ‚ fâ‚‚ h x).1 = fâ‚ x :=
  rfl

@[simp]
theorem snd_equivOfRightInverse (fâ‚ : M â†’L[R] Mâ‚‚) (fâ‚‚ : Mâ‚‚ â†’L[R] M)
    (h : Function.RightInverse fâ‚‚ fâ‚) (x : M) :
    ((equivOfRightInverse fâ‚ fâ‚‚ h x).2 : M) = x - fâ‚‚ (fâ‚ x) :=
  rfl

@[simp]
theorem equivOfRightInverse_symm_apply (fâ‚ : M â†’L[R] Mâ‚‚) (fâ‚‚ : Mâ‚‚ â†’L[R] M)
    (h : Function.RightInverse fâ‚‚ fâ‚) (y : Mâ‚‚ Ã— ker fâ‚) :
    (equivOfRightInverse fâ‚ fâ‚‚ h).symm y = fâ‚‚ y.1 + y.2 :=
  rfl

end Ring

end ContinuousLinearEquiv

namespace ContinuousLinearMap

variable {R : Type*} {M Mâ‚‚ Mâ‚ƒ : Type*}
  [TopologicalSpace M] [TopologicalSpace Mâ‚‚] [TopologicalSpace Mâ‚ƒ]

variable [Semiring R]
  [AddCommMonoid M] [Module R M]
  [AddCommMonoid Mâ‚‚] [Module R Mâ‚‚]
  [AddCommMonoid Mâ‚ƒ] [Module R Mâ‚ƒ]

/-- A continuous linear map is invertible if it is the forward direction of a continuous linear
equivalence. -/
def IsInvertible (f : M â†’L[R] Mâ‚‚) : Prop :=
  âˆƒ (A : M â‰ƒL[R] Mâ‚‚), A = f

open Classical in
/-- Introduce a function `inverse` from `M â†’L[R] Mâ‚‚` to `Mâ‚‚ â†’L[R] M`, which sends `f` to `f.symm` if
`f` is a continuous linear equivalence and to `0` otherwise.  This definition is somewhat ad hoc,
but one needs a fully (rather than partially) defined inverse function for some purposes, including
for calculus. -/
noncomputable def inverse : (M â†’L[R] Mâ‚‚) â†’ Mâ‚‚ â†’L[R] M := fun f =>
  if h : f.IsInvertible then ((Classical.choose h).symm : Mâ‚‚ â†’L[R] M) else 0

@[simp] lemma isInvertible_equiv {f : M â‰ƒL[R] Mâ‚‚} : IsInvertible (f : M â†’L[R] Mâ‚‚) := âŸ¨f, rflâŸ©

/-- By definition, if `f` is invertible then `inverse f = f.symm`. -/
@[simp]
theorem inverse_equiv (e : M â‰ƒL[R] Mâ‚‚) : inverse (e : M â†’L[R] Mâ‚‚) = e.symm := by
  simp [inverse]

/-- By definition, if `f` is not invertible then `inverse f = 0`. -/
@[simp] lemma inverse_of_not_isInvertible
    {f : M â†’L[R] Mâ‚‚} (hf : Â¬ f.IsInvertible) : f.inverse = 0 :=
  dif_neg hf

@[simp]
theorem isInvertible_zero_iff :
    IsInvertible (0 : M â†’L[R] Mâ‚‚) â†” Subsingleton M âˆ§ Subsingleton Mâ‚‚ := by
  refine âŸ¨fun âŸ¨e, heâŸ© â†¦ ?_, ?_âŸ©
  Â· have A : Subsingleton M := by
      refine âŸ¨fun x y â†¦ e.injective ?_âŸ©
      simp [he, â† ContinuousLinearEquiv.coe_coe]
    exact âŸ¨A, e.toEquiv.symm.subsingletonâŸ©
  Â· rintro âŸ¨hM, hMâ‚‚âŸ©
    let e : M â‰ƒL[R] Mâ‚‚ :=
    { toFun := 0
      invFun := 0
      left_inv x := Subsingleton.elim _ _
      right_inv x := Subsingleton.elim _ _
      map_add' x y := Subsingleton.elim _ _
      map_smul' c x := Subsingleton.elim _ _ }
    refine âŸ¨e, ?_âŸ©
    ext x
    exact Subsingleton.elim _ _

@[simp] theorem inverse_zero : inverse (0 : M â†’L[R] Mâ‚‚) = 0 := by
  by_cases h : IsInvertible (0 : M â†’L[R] Mâ‚‚)
  Â· rcases isInvertible_zero_iff.1 h with âŸ¨hM, hMâ‚‚âŸ©
    ext x
    exact Subsingleton.elim _ _
  Â· exact inverse_of_not_isInvertible h

lemma IsInvertible.comp {g : Mâ‚‚ â†’L[R] Mâ‚ƒ} {f : M â†’L[R] Mâ‚‚}
    (hg : g.IsInvertible) (hf : f.IsInvertible) : (g âˆ˜L f).IsInvertible := by
  rcases hg with âŸ¨N, rflâŸ©
  rcases hf with âŸ¨M, rflâŸ©
  exact âŸ¨M.trans N, rflâŸ©

lemma IsInvertible.of_inverse {f : M â†’L[R] Mâ‚‚} {g : Mâ‚‚ â†’L[R] M}
    (hf : f âˆ˜L g = id R Mâ‚‚) (hg : g âˆ˜L f = id R M) :
    f.IsInvertible :=
  âŸ¨ContinuousLinearEquiv.equivOfInverse' _ _ hf hg, rflâŸ©

lemma inverse_eq {f : M â†’L[R] Mâ‚‚} {g : Mâ‚‚ â†’L[R] M} (hf : f âˆ˜L g = id R Mâ‚‚) (hg : g âˆ˜L f = id R M) :
    f.inverse = g := by
  have : f = ContinuousLinearEquiv.equivOfInverse' f g hf hg := rfl
  rw [this, inverse_equiv]
  rfl

lemma IsInvertible.inverse_apply_eq {f : M â†’L[R] Mâ‚‚} {x : M} {y : Mâ‚‚} (hf : f.IsInvertible) :
    f.inverse y = x â†” y = f x := by
  rcases hf with âŸ¨M, rflâŸ©
  simp only [inverse_equiv, ContinuousLinearEquiv.coe_coe]
  exact ContinuousLinearEquiv.symm_apply_eq M

@[simp] lemma isInvertible_equiv_comp {e : Mâ‚‚ â‰ƒL[R] Mâ‚ƒ} {f : M â†’L[R] Mâ‚‚} :
    ((e : Mâ‚‚ â†’L[R] Mâ‚ƒ) âˆ˜L f).IsInvertible â†” f.IsInvertible := by
  constructor
  Â· rintro âŸ¨A, hAâŸ©
    have : f = e.symm âˆ˜L ((e : Mâ‚‚ â†’L[R] Mâ‚ƒ) âˆ˜L f) := by ext; simp
    rw [this, â† hA]
    simp
  Â· rintro âŸ¨M, rflâŸ©
    simp

@[simp] lemma isInvertible_comp_equiv {e : Mâ‚ƒ â‰ƒL[R] M} {f : M â†’L[R] Mâ‚‚} :
    (f âˆ˜L (e : Mâ‚ƒ â†’L[R] M)).IsInvertible â†” f.IsInvertible := by
  constructor
  Â· rintro âŸ¨A, hAâŸ©
    have : f = (f âˆ˜L (e : Mâ‚ƒ â†’L[R] M)) âˆ˜L e.symm := by ext; simp
    rw [this, â† hA]
    simp
  Â· rintro âŸ¨M, rflâŸ©
    simp

@[simp] lemma inverse_equiv_comp {e : Mâ‚‚ â‰ƒL[R] Mâ‚ƒ} {f : M â†’L[R] Mâ‚‚} :
    (e âˆ˜L f).inverse = f.inverse âˆ˜L (e.symm : Mâ‚ƒ â†’L[R] Mâ‚‚) := by
  by_cases hf : f.IsInvertible
  Â· rcases hf with âŸ¨A, rflâŸ©
    simp only [ContinuousLinearEquiv.comp_coe, inverse_equiv, ContinuousLinearEquiv.coe_inj]
    rfl
  Â· rw [inverse_of_not_isInvertible (by simp [hf]), inverse_of_not_isInvertible hf, zero_comp]

@[simp] lemma inverse_comp_equiv {e : Mâ‚ƒ â‰ƒL[R] M} {f : M â†’L[R] Mâ‚‚} :
    (f âˆ˜L e).inverse = (e.symm : M â†’L[R] Mâ‚ƒ) âˆ˜L f.inverse := by
  by_cases hf : f.IsInvertible
  Â· rcases hf with âŸ¨A, rflâŸ©
    simp only [ContinuousLinearEquiv.comp_coe, inverse_equiv, ContinuousLinearEquiv.coe_inj]
    rfl
  Â· rw [inverse_of_not_isInvertible (by simp [hf]), inverse_of_not_isInvertible hf, comp_zero]

lemma IsInvertible.inverse_comp_of_left {g : Mâ‚‚ â†’L[R] Mâ‚ƒ} {f : M â†’L[R] Mâ‚‚}
    (hg : g.IsInvertible) : (g âˆ˜L f).inverse = f.inverse âˆ˜L g.inverse := by
  rcases hg with âŸ¨N, rflâŸ©
  simp

lemma IsInvertible.inverse_comp_apply_of_left {g : Mâ‚‚ â†’L[R] Mâ‚ƒ} {f : M â†’L[R] Mâ‚‚} {v : Mâ‚ƒ}
    (hg : g.IsInvertible) : (g âˆ˜L f).inverse v = f.inverse (g.inverse v) := by
  simp only [hg.inverse_comp_of_left, coe_comp', Function.comp_apply]

lemma IsInvertible.inverse_comp_of_right {g : Mâ‚‚ â†’L[R] Mâ‚ƒ} {f : M â†’L[R] Mâ‚‚}
    (hf : f.IsInvertible) : (g âˆ˜L f).inverse = f.inverse âˆ˜L g.inverse := by
  rcases hf with âŸ¨M, rflâŸ©
  simp

lemma IsInvertible.inverse_comp_apply_of_right {g : Mâ‚‚ â†’L[R] Mâ‚ƒ} {f : M â†’L[R] Mâ‚‚} {v : Mâ‚ƒ}
    (hf : f.IsInvertible) : (g âˆ˜L f).inverse v = f.inverse (g.inverse v) := by
  simp only [hf.inverse_comp_of_right, coe_comp', Function.comp_apply]

@[simp]
theorem ringInverse_equiv (e : M â‰ƒL[R] M) : Ring.inverse â†‘e = inverse (e : M â†’L[R] M) := by
  suffices Ring.inverse ((ContinuousLinearEquiv.unitsEquiv _ _).symm e : M â†’L[R] M) = inverse â†‘e by
    convert this
  simp
  rfl

@[deprecated (since := "2025-04-22")] alias ring_inverse_equiv := ringInverse_equiv

/-- The function `ContinuousLinearEquiv.inverse` can be written in terms of `Ring.inverse` for the
ring of self-maps of the domain. -/
theorem inverse_eq_ringInverse (e : M â‰ƒL[R] Mâ‚‚) (f : M â†’L[R] Mâ‚‚) :
    inverse f = Ring.inverse ((e.symm : Mâ‚‚ â†’L[R] M).comp f) âˆ˜L e.symm := by
  by_cases hâ‚ : f.IsInvertible
  Â· obtain âŸ¨e', he'âŸ© := hâ‚
    rw [â† he']
    change _ = Ring.inverse (e'.trans e.symm : M â†’L[R] M) âˆ˜L (e.symm : Mâ‚‚ â†’L[R] M)
    ext
    simp
  Â· suffices Â¬IsUnit ((e.symm : Mâ‚‚ â†’L[R] M).comp f) by simp [this, hâ‚]
    contrapose! hâ‚
    rcases hâ‚ with âŸ¨F, hFâŸ©
    use (ContinuousLinearEquiv.unitsEquiv _ _ F).trans e
    ext
    dsimp
    rw [hF]
    simp

@[deprecated (since := "2025-04-22")] alias to_ring_inverse := inverse_eq_ringInverse

theorem ringInverse_eq_inverse : Ring.inverse = inverse (R := R) (M := M) := by
  ext
  simp [inverse_eq_ringInverse (ContinuousLinearEquiv.refl R M)]

@[deprecated (since := "2025-04-22")]
alias ring_inverse_eq_map_inverse := ringInverse_eq_inverse

@[simp] theorem inverse_id : (id R M).inverse = id R M := by
  rw [â† ringInverse_eq_inverse]
  exact Ring.inverse_one _

/-- Composition of a map on a product with the exchange of the product factors -/
theorem coprod_comp_prodComm [ContinuousAdd M] (f : Mâ‚‚ â†’L[R] M) (g : Mâ‚ƒ â†’L[R] M) :
    f.coprod g âˆ˜L ContinuousLinearEquiv.prodComm R Mâ‚ƒ Mâ‚‚ = g.coprod f := by
  ext <;> simp

end ContinuousLinearMap

namespace Submodule

variable {R : Type*} [Ring R] {M : Type*} [TopologicalSpace M] [AddCommGroup M] [Module R M]

open ContinuousLinearMap

/-- If `p` is a closed complemented submodule,
then there exists a submodule `q` and a continuous linear equivalence `M â‰ƒL[R] (p Ã— q)` such that
`e (x : p) = (x, 0)`, `e (y : q) = (0, y)`, and `e.symm x = x.1 + x.2`.

In fact, the properties of `e` imply the properties of `e.symm` and vice versa,
but we provide both for convenience. -/
lemma ClosedComplemented.exists_submodule_equiv_prod [IsTopologicalAddGroup M]
    {p : Submodule R M} (hp : p.ClosedComplemented) :
    âˆƒ (q : Submodule R M) (e : M â‰ƒL[R] (p Ã— q)),
      (âˆ€ x : p, e x = (x, 0)) âˆ§ (âˆ€ y : q, e y = (0, y)) âˆ§ (âˆ€ x, e.symm x = x.1 + x.2) :=
  let âŸ¨f, hfâŸ© := hp
  âŸ¨LinearMap.ker f, .equivOfRightInverse _ p.subtypeL hf,
    fun _ â†¦ by ext <;> simp [hf], fun _ â†¦ by ext <;> simp, fun _ â†¦ rflâŸ©

end Submodule

namespace MulOpposite

variable (R : Type*) [Semiring R] [Ï„R : TopologicalSpace R] [IsTopologicalSemiring R]
  {M : Type*} [AddCommMonoid M] [Module R M] [TopologicalSpace M] [ContinuousSMul R M]

/-- The function `op` is a continuous linear equivalence. -/
@[simps!]
def opContinuousLinearEquiv : M â‰ƒL[R] Máµáµ’áµ– where
  __ := MulOpposite.opLinearEquiv R

end MulOpposite

end
