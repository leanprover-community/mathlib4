/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, S√©bastien Gou√´zel, Jean Lo, Yury Kudryashov, Fr√©d√©ric Dupuis,
  Heather Macbeth
-/
module

public import Mathlib.Topology.Algebra.Module.LinearMapPiProd

/-!
# Continuous linear equivalences

Continuous semilinear / linear / star-linear equivalences between topological modules are denoted
by `M ‚âÉSL[œÉ] M‚ÇÇ`, `M ‚âÉL[R] M‚ÇÇ` and `M ‚âÉL‚ãÜ[R] M‚ÇÇ`.
-/

@[expose] public section

assert_not_exists TrivialStar

open LinearMap (ker range)
open Topology Filter Pointwise

universe u v w u'

section

/-- Continuous linear equivalences between modules. We only put the type classes that are necessary
for the definition, although in applications `M` and `M‚ÇÇ` will be topological modules over the
topological semiring `R`. -/
structure ContinuousLinearEquiv {R : Type*} {S : Type*} [Semiring R] [Semiring S] (œÉ : R ‚Üí+* S)
    {œÉ' : S ‚Üí+* R} [RingHomInvPair œÉ œÉ'] [RingHomInvPair œÉ' œÉ] (M : Type*) [TopologicalSpace M]
    [AddCommMonoid M] (M‚ÇÇ : Type*) [TopologicalSpace M‚ÇÇ] [AddCommMonoid M‚ÇÇ] [Module R M]
    [Module S M‚ÇÇ] extends M ‚âÉ‚Çõ‚Çó[œÉ] M‚ÇÇ where
  continuous_toFun : Continuous toFun := by continuity
  continuous_invFun : Continuous invFun := by continuity

attribute [inherit_doc ContinuousLinearEquiv] ContinuousLinearEquiv.continuous_toFun
ContinuousLinearEquiv.continuous_invFun

@[inherit_doc]
notation:50 M " ‚âÉSL[" œÉ "] " M‚ÇÇ => ContinuousLinearEquiv œÉ M M‚ÇÇ

@[inherit_doc]
notation:50 M " ‚âÉL[" R "] " M‚ÇÇ => ContinuousLinearEquiv (RingHom.id R) M M‚ÇÇ

/-- `ContinuousSemilinearEquivClass F œÉ M M‚ÇÇ` asserts `F` is a type of bundled continuous
`œÉ`-semilinear equivs `M ‚Üí M‚ÇÇ`.  See also `ContinuousLinearEquivClass F R M M‚ÇÇ` for the case
where `œÉ` is the identity map on `R`.  A map `f` between an `R`-module and an `S`-module over a ring
homomorphism `œÉ : R ‚Üí+* S` is semilinear if it satisfies the two properties `f (x + y) = f x + f y`
and `f (c ‚Ä¢ x) = (œÉ c) ‚Ä¢ f x`. -/
class ContinuousSemilinearEquivClass (F : Type*) {R : outParam Type*} {S : outParam Type*}
    [Semiring R] [Semiring S] (œÉ : outParam <| R ‚Üí+* S) {œÉ' : outParam <| S ‚Üí+* R}
    [RingHomInvPair œÉ œÉ'] [RingHomInvPair œÉ' œÉ] (M : outParam Type*) [TopologicalSpace M]
    [AddCommMonoid M] (M‚ÇÇ : outParam Type*) [TopologicalSpace M‚ÇÇ] [AddCommMonoid M‚ÇÇ] [Module R M]
    [Module S M‚ÇÇ] [EquivLike F M M‚ÇÇ] : Prop extends SemilinearEquivClass F œÉ M M‚ÇÇ where
  map_continuous : ‚àÄ f : F, Continuous f := by continuity
  inv_continuous : ‚àÄ f : F, Continuous (EquivLike.inv f) := by continuity

attribute [inherit_doc ContinuousSemilinearEquivClass]
ContinuousSemilinearEquivClass.map_continuous
ContinuousSemilinearEquivClass.inv_continuous

/-- `ContinuousLinearEquivClass F œÉ M M‚ÇÇ` asserts `F` is a type of bundled continuous
`R`-linear equivs `M ‚Üí M‚ÇÇ`. This is an abbreviation for
`ContinuousSemilinearEquivClass F (RingHom.id R) M M‚ÇÇ`. -/
abbrev ContinuousLinearEquivClass (F : Type*) (R : outParam Type*) [Semiring R]
    (M : outParam Type*) [TopologicalSpace M] [AddCommMonoid M] (M‚ÇÇ : outParam Type*)
    [TopologicalSpace M‚ÇÇ] [AddCommMonoid M‚ÇÇ] [Module R M] [Module R M‚ÇÇ] [EquivLike F M M‚ÇÇ] :=
  ContinuousSemilinearEquivClass F (RingHom.id R) M M‚ÇÇ

namespace ContinuousSemilinearEquivClass

variable (F : Type*) {R : Type*} {S : Type*} [Semiring R] [Semiring S] (œÉ : R ‚Üí+* S)
  {œÉ' : S ‚Üí+* R} [RingHomInvPair œÉ œÉ'] [RingHomInvPair œÉ' œÉ]
  (M : Type*) [TopologicalSpace M] [AddCommMonoid M]
  (M‚ÇÇ : Type*) [TopologicalSpace M‚ÇÇ] [AddCommMonoid M‚ÇÇ]
  [Module R M] [Module S M‚ÇÇ]

-- `œÉ'` becomes a metavariable, but it's OK since it's an outparam
instance (priority := 100) continuousSemilinearMapClass [EquivLike F M M‚ÇÇ]
    [s : ContinuousSemilinearEquivClass F œÉ M M‚ÇÇ] : ContinuousSemilinearMapClass F œÉ M M‚ÇÇ :=
  { s with }

instance (priority := 100) [EquivLike F M M‚ÇÇ]
    [s : ContinuousSemilinearEquivClass F œÉ M M‚ÇÇ] : HomeomorphClass F M M‚ÇÇ :=
  { s with }

end ContinuousSemilinearEquivClass

namespace ContinuousLinearMap

section Pi

variable {R : Type*} [Semiring R] {M : Type*} [TopologicalSpace M] [AddCommMonoid M] [Module R M]
  {M‚ÇÇ : Type*} [TopologicalSpace M‚ÇÇ] [AddCommMonoid M‚ÇÇ] [Module R M‚ÇÇ] {Œπ : Type*} {œÜ : Œπ ‚Üí Type*}
  [‚àÄ i, TopologicalSpace (œÜ i)] [‚àÄ i, AddCommMonoid (œÜ i)] [‚àÄ i, Module R (œÜ i)]

variable (R œÜ)

/-- If `I` and `J` are complementary index sets, the product of the kernels of the `J`th projections
of `œÜ` is linearly equivalent to the product over `I`. -/
def iInfKerProjEquiv {I J : Set Œπ} [DecidablePred fun i => i ‚àà I] (hd : Disjoint I J)
    (hu : Set.univ ‚äÜ I ‚à™ J) :
    (‚®Ö i ‚àà J, (proj i : (‚àÄ i, œÜ i) ‚ÜíL[R] œÜ i).ker : Submodule R (‚àÄ i, œÜ i)) ‚âÉL[R] ‚àÄ i : I, œÜ i where
  toLinearEquiv := LinearMap.iInfKerProjEquiv R œÜ hd hu
  continuous_toFun :=
    continuous_pi fun i =>
      Continuous.comp (continuous_apply (A := œÜ) i) <| continuous_subtype_val
  continuous_invFun :=
    Continuous.subtype_mk
      (continuous_pi fun i => by
        dsimp
        split_ifs <;> [apply continuous_apply; exact continuous_zero])
      _

end Pi

end ContinuousLinearMap

namespace ContinuousLinearEquiv

section AddCommMonoid

variable {R‚ÇÅ : Type*} {R‚ÇÇ : Type*} {R‚ÇÉ : Type*} [Semiring R‚ÇÅ] [Semiring R‚ÇÇ] [Semiring R‚ÇÉ]
  {œÉ‚ÇÅ‚ÇÇ : R‚ÇÅ ‚Üí+* R‚ÇÇ} {œÉ‚ÇÇ‚ÇÅ : R‚ÇÇ ‚Üí+* R‚ÇÅ} [RingHomInvPair œÉ‚ÇÅ‚ÇÇ œÉ‚ÇÇ‚ÇÅ] [RingHomInvPair œÉ‚ÇÇ‚ÇÅ œÉ‚ÇÅ‚ÇÇ]
  {œÉ‚ÇÇ‚ÇÉ : R‚ÇÇ ‚Üí+* R‚ÇÉ} {œÉ‚ÇÉ‚ÇÇ : R‚ÇÉ ‚Üí+* R‚ÇÇ} [RingHomInvPair œÉ‚ÇÇ‚ÇÉ œÉ‚ÇÉ‚ÇÇ] [RingHomInvPair œÉ‚ÇÉ‚ÇÇ œÉ‚ÇÇ‚ÇÉ]
  {œÉ‚ÇÅ‚ÇÉ : R‚ÇÅ ‚Üí+* R‚ÇÉ} {œÉ‚ÇÉ‚ÇÅ : R‚ÇÉ ‚Üí+* R‚ÇÅ} [RingHomInvPair œÉ‚ÇÅ‚ÇÉ œÉ‚ÇÉ‚ÇÅ] [RingHomInvPair œÉ‚ÇÉ‚ÇÅ œÉ‚ÇÅ‚ÇÉ]
  [RingHomCompTriple œÉ‚ÇÅ‚ÇÇ œÉ‚ÇÇ‚ÇÉ œÉ‚ÇÅ‚ÇÉ] [RingHomCompTriple œÉ‚ÇÉ‚ÇÇ œÉ‚ÇÇ‚ÇÅ œÉ‚ÇÉ‚ÇÅ] {M‚ÇÅ : Type*}
  [TopologicalSpace M‚ÇÅ] [AddCommMonoid M‚ÇÅ]
  {M‚ÇÇ : Type*} [TopologicalSpace M‚ÇÇ] [AddCommMonoid M‚ÇÇ] {M‚ÇÉ : Type*} [TopologicalSpace M‚ÇÉ]
  [AddCommMonoid M‚ÇÉ] {M‚ÇÑ : Type*} [TopologicalSpace M‚ÇÑ] [AddCommMonoid M‚ÇÑ] [Module R‚ÇÅ M‚ÇÅ]
  [Module R‚ÇÇ M‚ÇÇ] [Module R‚ÇÉ M‚ÇÉ]

/-- A continuous linear equivalence induces a continuous linear map. -/
@[coe]
def toContinuousLinearMap (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) : M‚ÇÅ ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ :=
  { e.toLinearEquiv.toLinearMap with cont := e.continuous_toFun }

/-- Coerce continuous linear equivs to continuous linear maps. -/
instance ContinuousLinearMap.coe : Coe (M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (M‚ÇÅ ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) :=
  ‚ü®toContinuousLinearMap‚ü©

instance equivLike :
    EquivLike (M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) M‚ÇÅ M‚ÇÇ where
  coe f := f.toFun
  inv f := f.invFun
  coe_injective' f g h‚ÇÅ h‚ÇÇ := by
    obtain ‚ü®f', _‚ü© := f
    obtain ‚ü®g', _‚ü© := g
    rcases f' with ‚ü®‚ü®‚ü®_, _‚ü©, _‚ü©, _‚ü©
    rcases g' with ‚ü®‚ü®‚ü®_, _‚ü©, _‚ü©, _‚ü©
    congr
  left_inv f := f.left_inv
  right_inv f := f.right_inv

instance continuousSemilinearEquivClass :
    ContinuousSemilinearEquivClass (M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) œÉ‚ÇÅ‚ÇÇ M‚ÇÅ M‚ÇÇ where
  map_add f := f.map_add'
  map_smul‚Çõ‚Çó f := f.map_smul'
  map_continuous := continuous_toFun
  inv_continuous := continuous_invFun

@[simp]
theorem coe_mk (e : M‚ÇÅ ‚âÉ‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (a b) : ‚áë(ContinuousLinearEquiv.mk e a b) = e := rfl

theorem coe_apply (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (b : M‚ÇÅ) : (e : M‚ÇÅ ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) b = e b :=
  rfl

@[simp]
theorem coe_toLinearEquiv (f : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) : ‚áëf.toLinearEquiv = f :=
  rfl

@[simp, norm_cast]
theorem coe_coe (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) : ‚áë(e : M‚ÇÅ ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) = e :=
  rfl

theorem toLinearEquiv_injective :
    Function.Injective (toLinearEquiv : (M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) ‚Üí M‚ÇÅ ‚âÉ‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) := by
  rintro ‚ü®e, _, _‚ü© ‚ü®e', _, _‚ü© rfl
  rfl

@[ext]
theorem ext {f g : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ} (h : (f : M‚ÇÅ ‚Üí M‚ÇÇ) = g) : f = g :=
  toLinearEquiv_injective <| LinearEquiv.ext <| congr_fun h

theorem coe_injective : Function.Injective ((‚Üë) : (M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) ‚Üí M‚ÇÅ ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) :=
  fun _e _e' h => ext <| funext <| ContinuousLinearMap.ext_iff.1 h

@[simp, norm_cast]
theorem coe_inj {e e' : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ} : (e : M‚ÇÅ ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) = e' ‚Üî e = e' :=
  coe_injective.eq_iff

/-- A continuous linear equivalence induces a homeomorphism. -/
def toHomeomorph (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) : M‚ÇÅ ‚âÉ‚Çú M‚ÇÇ :=
  { e with toEquiv := e.toLinearEquiv.toEquiv }

@[simp]
theorem coe_toHomeomorph (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) : ‚áëe.toHomeomorph = e :=
  rfl

theorem isOpenMap (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) : IsOpenMap e :=
  (ContinuousLinearEquiv.toHomeomorph e).isOpenMap

theorem image_closure (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (s : Set M‚ÇÅ) : e '' closure s = closure (e '' s) :=
  e.toHomeomorph.image_closure s

theorem preimage_closure (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (s : Set M‚ÇÇ) : e ‚Åª¬π' closure s = closure (e ‚Åª¬π' s) :=
  e.toHomeomorph.preimage_closure s

@[simp]
theorem isClosed_image (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) {s : Set M‚ÇÅ} : IsClosed (e '' s) ‚Üî IsClosed s :=
  e.toHomeomorph.isClosed_image

theorem map_nhds_eq (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (x : M‚ÇÅ) : map e (ùìù x) = ùìù (e x) :=
  e.toHomeomorph.map_nhds_eq x

-- Make some straightforward lemmas available to `simp`.
theorem map_zero (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) : e (0 : M‚ÇÅ) = 0 :=
  (e : M‚ÇÅ ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ).map_zero

theorem map_add (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (x y : M‚ÇÅ) : e (x + y) = e x + e y :=
  (e : M‚ÇÅ ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ).map_add x y

@[simp]
theorem map_smul‚Çõ‚Çó (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (c : R‚ÇÅ) (x : M‚ÇÅ) : e (c ‚Ä¢ x) = œÉ‚ÇÅ‚ÇÇ c ‚Ä¢ e x :=
  (e : M‚ÇÅ ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ).map_smul‚Çõ‚Çó c x

theorem map_smul [Module R‚ÇÅ M‚ÇÇ] (e : M‚ÇÅ ‚âÉL[R‚ÇÅ] M‚ÇÇ) (c : R‚ÇÅ) (x : M‚ÇÅ) : e (c ‚Ä¢ x) = c ‚Ä¢ e x :=
  (e : M‚ÇÅ ‚ÜíL[R‚ÇÅ] M‚ÇÇ).map_smul c x

theorem map_eq_zero_iff (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) {x : M‚ÇÅ} : e x = 0 ‚Üî x = 0 :=
  e.toLinearEquiv.map_eq_zero_iff

attribute [continuity]
  ContinuousLinearEquiv.continuous_toFun ContinuousLinearEquiv.continuous_invFun

@[continuity]
protected theorem continuous (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) : Continuous (e : M‚ÇÅ ‚Üí M‚ÇÇ) :=
  e.continuous_toFun

protected theorem continuousOn (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) {s : Set M‚ÇÅ} : ContinuousOn (e : M‚ÇÅ ‚Üí M‚ÇÇ) s :=
  e.continuous.continuousOn

protected theorem continuousAt (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) {x : M‚ÇÅ} : ContinuousAt (e : M‚ÇÅ ‚Üí M‚ÇÇ) x :=
  e.continuous.continuousAt

protected theorem continuousWithinAt (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) {s : Set M‚ÇÅ} {x : M‚ÇÅ} :
    ContinuousWithinAt (e : M‚ÇÅ ‚Üí M‚ÇÇ) s x :=
  e.continuous.continuousWithinAt

theorem comp_continuousOn_iff {Œ± : Type*} [TopologicalSpace Œ±] (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) {f : Œ± ‚Üí M‚ÇÅ}
    {s : Set Œ±} : ContinuousOn (e ‚àò f) s ‚Üî ContinuousOn f s :=
  e.toHomeomorph.comp_continuousOn_iff _ _

theorem comp_continuous_iff {Œ± : Type*} [TopologicalSpace Œ±] (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) {f : Œ± ‚Üí M‚ÇÅ} :
    Continuous (e ‚àò f) ‚Üî Continuous f :=
  e.toHomeomorph.comp_continuous_iff

/-- An extensionality lemma for `R ‚âÉL[R] M`. -/
theorem ext‚ÇÅ [TopologicalSpace R‚ÇÅ] {f g : R‚ÇÅ ‚âÉL[R‚ÇÅ] M‚ÇÅ} (h : f 1 = g 1) : f = g :=
  ext <| funext fun x => mul_one x ‚ñ∏ by rw [‚Üê smul_eq_mul, map_smul, h, map_smul]

section

variable (R‚ÇÅ M‚ÇÅ)

/-- The identity map as a continuous linear equivalence. -/
@[refl]
protected def refl : M‚ÇÅ ‚âÉL[R‚ÇÅ] M‚ÇÅ :=
  { LinearEquiv.refl R‚ÇÅ M‚ÇÅ with
    continuous_toFun := continuous_id
    continuous_invFun := continuous_id }

@[simp]
theorem refl_apply (x : M‚ÇÅ) :
    ContinuousLinearEquiv.refl R‚ÇÅ M‚ÇÅ x = x := rfl

end

@[simp, norm_cast]
theorem coe_refl : ‚Üë(ContinuousLinearEquiv.refl R‚ÇÅ M‚ÇÅ) = ContinuousLinearMap.id R‚ÇÅ M‚ÇÅ :=
  rfl

@[simp, norm_cast]
theorem coe_refl' : ‚áë(ContinuousLinearEquiv.refl R‚ÇÅ M‚ÇÅ) = id :=
  rfl

/-- The inverse of a continuous linear equivalence as a continuous linear equivalence -/
@[symm]
protected def symm (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) : M‚ÇÇ ‚âÉSL[œÉ‚ÇÇ‚ÇÅ] M‚ÇÅ :=
  { e.toLinearEquiv.symm with
    continuous_toFun := e.continuous_invFun
    continuous_invFun := e.continuous_toFun }

@[simp]
theorem toLinearEquiv_symm (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) : e.symm.toLinearEquiv = e.toLinearEquiv.symm :=
  rfl

@[simp]
theorem coe_symm_toLinearEquiv (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) : ‚áëe.toLinearEquiv.symm = e.symm :=
  rfl

@[simp]
theorem toHomeomorph_symm (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) : e.symm.toHomeomorph = e.toHomeomorph.symm :=
  rfl

@[simp]
theorem coe_symm_toHomeomorph (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) : ‚áëe.toHomeomorph.symm = e.symm :=
  rfl

/-- See Note [custom simps projection]. We need to specify this projection explicitly in this case,
  because it is a composition of multiple projections. -/
def Simps.apply (h : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) : M‚ÇÅ ‚Üí M‚ÇÇ :=
  h

/-- See Note [custom simps projection] -/
def Simps.symm_apply (h : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) : M‚ÇÇ ‚Üí M‚ÇÅ :=
  h.symm

initialize_simps_projections ContinuousLinearEquiv (toFun ‚Üí apply, invFun ‚Üí symm_apply)

theorem symm_map_nhds_eq (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (x : M‚ÇÅ) : map e.symm (ùìù (e x)) = ùìù x :=
  e.toHomeomorph.symm_map_nhds_eq x

/-- The composition of two continuous linear equivalences as a continuous linear equivalence. -/
@[trans]
protected def trans (e‚ÇÅ : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (e‚ÇÇ : M‚ÇÇ ‚âÉSL[œÉ‚ÇÇ‚ÇÉ] M‚ÇÉ) : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÉ] M‚ÇÉ :=
  { e‚ÇÅ.toLinearEquiv.trans e‚ÇÇ.toLinearEquiv with
    continuous_toFun := e‚ÇÇ.continuous_toFun.comp e‚ÇÅ.continuous_toFun
    continuous_invFun := e‚ÇÅ.continuous_invFun.comp e‚ÇÇ.continuous_invFun }

@[simp]
theorem trans_toLinearEquiv (e‚ÇÅ : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (e‚ÇÇ : M‚ÇÇ ‚âÉSL[œÉ‚ÇÇ‚ÇÉ] M‚ÇÉ) :
    (e‚ÇÅ.trans e‚ÇÇ).toLinearEquiv = e‚ÇÅ.toLinearEquiv.trans e‚ÇÇ.toLinearEquiv := by
  ext
  rfl

/-- Product of two continuous linear equivalences. The map comes from `Equiv.prodCongr`. -/
def prodCongr [Module R‚ÇÅ M‚ÇÇ] [Module R‚ÇÅ M‚ÇÉ] [Module R‚ÇÅ M‚ÇÑ] (e : M‚ÇÅ ‚âÉL[R‚ÇÅ] M‚ÇÇ) (e' : M‚ÇÉ ‚âÉL[R‚ÇÅ] M‚ÇÑ) :
    (M‚ÇÅ √ó M‚ÇÉ) ‚âÉL[R‚ÇÅ] M‚ÇÇ √ó M‚ÇÑ :=
  { e.toLinearEquiv.prodCongr e'.toLinearEquiv with
    continuous_toFun := e.continuous_toFun.prodMap e'.continuous_toFun
    continuous_invFun := e.continuous_invFun.prodMap e'.continuous_invFun }

@[simp, norm_cast]
theorem prodCongr_apply [Module R‚ÇÅ M‚ÇÇ] [Module R‚ÇÅ M‚ÇÉ] [Module R‚ÇÅ M‚ÇÑ] (e : M‚ÇÅ ‚âÉL[R‚ÇÅ] M‚ÇÇ)
    (e' : M‚ÇÉ ‚âÉL[R‚ÇÅ] M‚ÇÑ) (x) : e.prodCongr e' x = (e x.1, e' x.2) :=
  rfl

@[simp, norm_cast]
theorem coe_prodCongr [Module R‚ÇÅ M‚ÇÇ] [Module R‚ÇÅ M‚ÇÉ] [Module R‚ÇÅ M‚ÇÑ] (e : M‚ÇÅ ‚âÉL[R‚ÇÅ] M‚ÇÇ)
    (e' : M‚ÇÉ ‚âÉL[R‚ÇÅ] M‚ÇÑ) :
    (e.prodCongr e' : M‚ÇÅ √ó M‚ÇÉ ‚ÜíL[R‚ÇÅ] M‚ÇÇ √ó M‚ÇÑ) = (e : M‚ÇÅ ‚ÜíL[R‚ÇÅ] M‚ÇÇ).prodMap (e' : M‚ÇÉ ‚ÜíL[R‚ÇÅ] M‚ÇÑ) :=
  rfl

theorem prodCongr_symm [Module R‚ÇÅ M‚ÇÇ] [Module R‚ÇÅ M‚ÇÉ] [Module R‚ÇÅ M‚ÇÑ] (e : M‚ÇÅ ‚âÉL[R‚ÇÅ] M‚ÇÇ)
    (e' : M‚ÇÉ ‚âÉL[R‚ÇÅ] M‚ÇÑ) : (e.prodCongr e').symm = e.symm.prodCongr e'.symm :=
  rfl

variable (R‚ÇÅ M‚ÇÅ M‚ÇÇ)

/-- Product of modules is commutative up to continuous linear isomorphism. -/
@[simps! apply toLinearEquiv]
def prodComm [Module R‚ÇÅ M‚ÇÇ] : (M‚ÇÅ √ó M‚ÇÇ) ‚âÉL[R‚ÇÅ] M‚ÇÇ √ó M‚ÇÅ :=
  { LinearEquiv.prodComm R‚ÇÅ M‚ÇÅ M‚ÇÇ with
    continuous_toFun := continuous_swap
    continuous_invFun := continuous_swap }

@[simp] lemma prodComm_symm [Module R‚ÇÅ M‚ÇÇ] : (prodComm R‚ÇÅ M‚ÇÅ M‚ÇÇ).symm = prodComm R‚ÇÅ M‚ÇÇ M‚ÇÅ := rfl

section prodAssoc

variable (R M‚ÇÅ M‚ÇÇ M‚ÇÉ : Type*) [Semiring R]
  [AddCommMonoid M‚ÇÅ] [AddCommMonoid M‚ÇÇ] [AddCommMonoid M‚ÇÉ] [Module R M‚ÇÅ] [Module R M‚ÇÇ] [Module R M‚ÇÉ]
  [TopologicalSpace M‚ÇÅ] [TopologicalSpace M‚ÇÇ] [TopologicalSpace M‚ÇÉ]

/-- The product of topological modules is associative up to continuous linear isomorphism.
This is `LinearEquiv.prodAssoc` prodAssoc as a continuous linear equivalence. -/
def prodAssoc : ((M‚ÇÅ √ó M‚ÇÇ) √ó M‚ÇÉ) ‚âÉL[R] M‚ÇÅ √ó M‚ÇÇ √ó M‚ÇÉ where
  toLinearEquiv := LinearEquiv.prodAssoc R M‚ÇÅ M‚ÇÇ M‚ÇÉ
  continuous_toFun := (continuous_fst.comp continuous_fst).prodMk
    ((continuous_snd.comp continuous_fst).prodMk continuous_snd)
  continuous_invFun := (continuous_fst.prodMk (continuous_fst.comp continuous_snd)).prodMk
    (continuous_snd.comp continuous_snd)

@[simp]
lemma prodAssoc_toLinearEquiv :
    (prodAssoc R M‚ÇÅ M‚ÇÇ M‚ÇÉ).toLinearEquiv = LinearEquiv.prodAssoc R M‚ÇÅ M‚ÇÇ M‚ÇÉ := rfl

@[simp]
lemma coe_prodAssoc :
    (prodAssoc R M‚ÇÅ M‚ÇÇ M‚ÇÉ : (M‚ÇÅ √ó M‚ÇÇ) √ó M‚ÇÉ ‚Üí M‚ÇÅ √ó M‚ÇÇ √ó M‚ÇÉ) = Equiv.prodAssoc M‚ÇÅ M‚ÇÇ M‚ÇÉ := rfl

@[simp]
lemma prodAssoc_apply (p‚ÇÅ : M‚ÇÅ) (p‚ÇÇ : M‚ÇÇ) (p‚ÇÉ : M‚ÇÉ) :
    prodAssoc R M‚ÇÅ M‚ÇÇ M‚ÇÉ ((p‚ÇÅ, p‚ÇÇ), p‚ÇÉ) = (p‚ÇÅ, (p‚ÇÇ, p‚ÇÉ)) := rfl

@[simp]
lemma prodAssoc_symm_apply (p‚ÇÅ : M‚ÇÅ) (p‚ÇÇ : M‚ÇÇ) (p‚ÇÉ : M‚ÇÉ) :
    (prodAssoc R M‚ÇÅ M‚ÇÇ M‚ÇÉ).symm (p‚ÇÅ, (p‚ÇÇ, p‚ÇÉ)) = ((p‚ÇÅ, p‚ÇÇ), p‚ÇÉ) := rfl

end prodAssoc

section prodProdProdComm

variable (R M‚ÇÅ M‚ÇÇ M‚ÇÉ M‚ÇÑ : Type*) [Semiring R]
  [AddCommMonoid M‚ÇÅ] [AddCommMonoid M‚ÇÇ] [AddCommMonoid M‚ÇÉ] [AddCommMonoid M‚ÇÑ]
  [Module R M‚ÇÅ] [Module R M‚ÇÇ] [Module R M‚ÇÉ] [Module R M‚ÇÑ]
  [TopologicalSpace M‚ÇÅ] [TopologicalSpace M‚ÇÇ] [TopologicalSpace M‚ÇÉ] [TopologicalSpace M‚ÇÑ]

/-- The product of topological modules is four-way commutative up to continuous linear isomorphism.
This is `LinearEquiv.prodProdProdComm` prodAssoc as a continuous linear equivalence. -/
def prodProdProdComm : ((M‚ÇÅ √ó M‚ÇÇ) √ó M‚ÇÉ √ó M‚ÇÑ) ‚âÉL[R] (M‚ÇÅ √ó M‚ÇÉ) √ó M‚ÇÇ √ó M‚ÇÑ where
  toLinearEquiv := LinearEquiv.prodProdProdComm R M‚ÇÅ M‚ÇÇ M‚ÇÉ M‚ÇÑ
  continuous_toFun := by fun_prop
  continuous_invFun := by fun_prop

@[simp]
theorem prodProdProdComm_symm :
    (prodProdProdComm R M‚ÇÅ M‚ÇÇ M‚ÇÉ M‚ÇÑ).symm = prodProdProdComm R M‚ÇÅ M‚ÇÉ M‚ÇÇ M‚ÇÑ :=
  rfl

@[simp]
lemma prodProdProdComm_toLinearEquiv :
    (prodProdProdComm R M‚ÇÅ M‚ÇÇ M‚ÇÉ M‚ÇÑ).toLinearEquiv = LinearEquiv.prodProdProdComm R M‚ÇÅ M‚ÇÇ M‚ÇÉ M‚ÇÑ :=
  rfl

@[simp]
lemma coe_prodProdProdComm :
    (prodProdProdComm R M‚ÇÅ M‚ÇÇ M‚ÇÉ M‚ÇÑ : (M‚ÇÅ √ó M‚ÇÇ) √ó M‚ÇÉ √ó M‚ÇÑ ‚Üí (M‚ÇÅ √ó M‚ÇÉ) √ó M‚ÇÇ √ó M‚ÇÑ) =
      Equiv.prodProdProdComm M‚ÇÅ M‚ÇÇ M‚ÇÉ M‚ÇÑ := rfl

@[simp]
lemma prodProdProdComm_apply (p‚ÇÅ : M‚ÇÅ) (p‚ÇÇ : M‚ÇÇ) (p‚ÇÉ : M‚ÇÉ) (p‚ÇÑ : M‚ÇÑ) :
    prodProdProdComm R M‚ÇÅ M‚ÇÇ M‚ÇÉ M‚ÇÑ ((p‚ÇÅ, p‚ÇÇ), p‚ÇÉ, p‚ÇÑ) = ((p‚ÇÅ, p‚ÇÉ), p‚ÇÇ, p‚ÇÑ) := rfl

end prodProdProdComm

section prodUnique

variable (R M N : Type*) [Semiring R]
  [TopologicalSpace M] [AddCommMonoid M] [TopologicalSpace N] [AddCommMonoid N]
  [Unique N] [Module R M] [Module R N]

/-- The natural equivalence `M √ó N ‚âÉL[R] M` for any `Unique` type `N`.
This is `Equiv.prodUnique` as a continuous linear equivalence. -/
def prodUnique : (M √ó N) ‚âÉL[R] M where
  toLinearEquiv := LinearEquiv.prodUnique
  continuous_toFun := by
    change Continuous (Equiv.prodUnique M N)
    dsimp; fun_prop
  continuous_invFun := by
    change Continuous fun x ‚Ü¶ (x, default)
    fun_prop

@[simp]
lemma coe_prodUnique : (prodUnique R M N).toEquiv = Equiv.prodUnique M N := rfl

@[simp]
lemma prodUnique_apply (x : M √ó N) : prodUnique R M N x = x.1 := rfl

@[simp]
lemma prodUnique_symm_apply (x : M) : (prodUnique R M N).symm x = (x, default) := rfl

/-- The natural equivalence `N √ó M ‚âÉL[R] M` for any `Unique` type `N`.
This is `Equiv.uniqueProd` as a continuous linear equivalence. -/
def uniqueProd : (N √ó M) ‚âÉL[R] M where
  toLinearEquiv := LinearEquiv.uniqueProd
  continuous_toFun := by
    change Continuous (Equiv.uniqueProd M N)
    dsimp; fun_prop
  continuous_invFun := by
    change Continuous fun x ‚Ü¶ (default, x)
    fun_prop

@[simp]
lemma coe_uniqueProd : (uniqueProd R M N).toEquiv = Equiv.uniqueProd M N := rfl

@[simp]
lemma uniqueProd_apply (x : N √ó M) : uniqueProd R M N x = x.2 := rfl

@[simp]
lemma uniqueProd_symm_apply (x : M) : (uniqueProd R M N).symm x = (default, x) := rfl

end prodUnique

variable {R‚ÇÅ M‚ÇÅ M‚ÇÇ}

protected theorem bijective (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) : Function.Bijective e :=
  e.toLinearEquiv.toEquiv.bijective

protected theorem injective (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) : Function.Injective e :=
  e.toLinearEquiv.toEquiv.injective

protected theorem surjective (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) : Function.Surjective e :=
  e.toLinearEquiv.toEquiv.surjective

@[simp]
theorem trans_apply (e‚ÇÅ : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (e‚ÇÇ : M‚ÇÇ ‚âÉSL[œÉ‚ÇÇ‚ÇÉ] M‚ÇÉ) (c : M‚ÇÅ) :
    (e‚ÇÅ.trans e‚ÇÇ) c = e‚ÇÇ (e‚ÇÅ c) :=
  rfl

@[simp]
theorem apply_symm_apply (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (c : M‚ÇÇ) : e (e.symm c) = c :=
  e.1.right_inv c

@[simp]
theorem symm_apply_apply (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (b : M‚ÇÅ) : e.symm (e b) = b :=
  e.1.left_inv b

@[simp] theorem symm_trans_self (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) : e.symm.trans e = .refl R‚ÇÇ M‚ÇÇ :=
  ext <| funext fun _ ‚Ü¶ apply_symm_apply _ _

@[simp] theorem self_trans_symm (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) : e.trans e.symm = .refl R‚ÇÅ M‚ÇÅ :=
  ext <| funext fun _ ‚Ü¶ symm_apply_apply _ _

@[simp]
theorem symm_trans_apply (e‚ÇÅ : M‚ÇÇ ‚âÉSL[œÉ‚ÇÇ‚ÇÅ] M‚ÇÅ) (e‚ÇÇ : M‚ÇÉ ‚âÉSL[œÉ‚ÇÉ‚ÇÇ] M‚ÇÇ) (c : M‚ÇÅ) :
    (e‚ÇÇ.trans e‚ÇÅ).symm c = e‚ÇÇ.symm (e‚ÇÅ.symm c) :=
  rfl

@[simp]
theorem symm_image_image (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (s : Set M‚ÇÅ) : e.symm '' (e '' s) = s :=
  e.toLinearEquiv.toEquiv.symm_image_image s

@[simp]
theorem image_symm_image (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (s : Set M‚ÇÇ) : e '' (e.symm '' s) = s :=
  e.symm.symm_image_image s

@[simp, norm_cast]
theorem comp_coe (f : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (f' : M‚ÇÇ ‚âÉSL[œÉ‚ÇÇ‚ÇÉ] M‚ÇÉ) :
    (f' : M‚ÇÇ ‚ÜíSL[œÉ‚ÇÇ‚ÇÉ] M‚ÇÉ).comp (f : M‚ÇÅ ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) = (f.trans f' : M‚ÇÅ ‚ÜíSL[œÉ‚ÇÅ‚ÇÉ] M‚ÇÉ) :=
  rfl

-- The priority should be higher than `comp_coe`.
@[simp high]
theorem coe_comp_coe_symm (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) :
    (e : M‚ÇÅ ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ).comp (e.symm : M‚ÇÇ ‚ÜíSL[œÉ‚ÇÇ‚ÇÅ] M‚ÇÅ) = ContinuousLinearMap.id R‚ÇÇ M‚ÇÇ :=
  ContinuousLinearMap.ext e.apply_symm_apply

-- The priority should be higher than `comp_coe`.
@[simp high]
theorem coe_symm_comp_coe (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) :
    (e.symm : M‚ÇÇ ‚ÜíSL[œÉ‚ÇÇ‚ÇÅ] M‚ÇÅ).comp (e : M‚ÇÅ ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) = ContinuousLinearMap.id R‚ÇÅ M‚ÇÅ :=
  ContinuousLinearMap.ext e.symm_apply_apply

@[simp]
theorem symm_comp_self (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) : (e.symm : M‚ÇÇ ‚Üí M‚ÇÅ) ‚àò (e : M‚ÇÅ ‚Üí M‚ÇÇ) = id := by
  ext x
  exact symm_apply_apply e x

@[simp]
theorem self_comp_symm (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) : (e : M‚ÇÅ ‚Üí M‚ÇÇ) ‚àò (e.symm : M‚ÇÇ ‚Üí M‚ÇÅ) = id := by
  ext x
  exact apply_symm_apply e x

@[simp]
theorem symm_symm (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) : e.symm.symm = e := rfl

theorem symm_bijective : Function.Bijective (ContinuousLinearEquiv.symm : (M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) ‚Üí _) :=
  Function.bijective_iff_has_inverse.mpr ‚ü®_, symm_symm, symm_symm‚ü©

@[simp]
theorem refl_symm : (ContinuousLinearEquiv.refl R‚ÇÅ M‚ÇÅ).symm = ContinuousLinearEquiv.refl R‚ÇÅ M‚ÇÅ :=
  rfl

theorem symm_symm_apply (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (x : M‚ÇÅ) : e.symm.symm x = e x :=
  rfl

theorem symm_apply_eq (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) {x y} : e.symm x = y ‚Üî x = e y :=
  e.toLinearEquiv.symm_apply_eq

theorem eq_symm_apply (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) {x y} : y = e.symm x ‚Üî e y = x :=
  e.toLinearEquiv.eq_symm_apply

protected lemma image_eq_preimage_symm (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (s : Set M‚ÇÅ) : e '' s = e.symm ‚Åª¬π' s :=
  e.toLinearEquiv.toEquiv.image_eq_preimage_symm s

protected theorem image_symm_eq_preimage (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (s : Set M‚ÇÇ) :
    e.symm '' s = e ‚Åª¬π' s := by rw [e.symm.image_eq_preimage_symm, e.symm_symm]

@[simp]
protected theorem symm_preimage_preimage (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (s : Set M‚ÇÇ) :
    e.symm ‚Åª¬π' (e ‚Åª¬π' s) = s :=
  e.toLinearEquiv.toEquiv.symm_preimage_preimage s

@[simp]
protected theorem preimage_symm_preimage (e : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (s : Set M‚ÇÅ) :
    e ‚Åª¬π' (e.symm ‚Åª¬π' s) = s :=
  e.symm.symm_preimage_preimage s

lemma isUniformEmbedding {E‚ÇÅ E‚ÇÇ : Type*} [UniformSpace E‚ÇÅ] [UniformSpace E‚ÇÇ]
    [AddCommGroup E‚ÇÅ] [AddCommGroup E‚ÇÇ] [Module R‚ÇÅ E‚ÇÅ] [Module R‚ÇÇ E‚ÇÇ] [IsUniformAddGroup E‚ÇÅ]
    [IsUniformAddGroup E‚ÇÇ] (e : E‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) : IsUniformEmbedding e :=
  e.toLinearEquiv.toEquiv.isUniformEmbedding e.toContinuousLinearMap.uniformContinuous
    e.symm.toContinuousLinearMap.uniformContinuous

protected theorem _root_.LinearEquiv.isUniformEmbedding {E‚ÇÅ E‚ÇÇ : Type*} [UniformSpace E‚ÇÅ]
    [UniformSpace E‚ÇÇ] [AddCommGroup E‚ÇÅ] [AddCommGroup E‚ÇÇ] [Module R‚ÇÅ E‚ÇÅ] [Module R‚ÇÇ E‚ÇÇ]
    [IsUniformAddGroup E‚ÇÅ] [IsUniformAddGroup E‚ÇÇ] (e : E‚ÇÅ ‚âÉ‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ)
    (h‚ÇÅ : Continuous e) (h‚ÇÇ : Continuous e.symm) : IsUniformEmbedding e :=
  ContinuousLinearEquiv.isUniformEmbedding
    ({ e with
        continuous_toFun := h‚ÇÅ
        continuous_invFun := h‚ÇÇ } :
      E‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ)

/-- Create a `ContinuousLinearEquiv` from two `ContinuousLinearMap`s that are
inverse of each other. See also `equivOfInverse'`. -/
def equivOfInverse (f‚ÇÅ : M‚ÇÅ ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (f‚ÇÇ : M‚ÇÇ ‚ÜíSL[œÉ‚ÇÇ‚ÇÅ] M‚ÇÅ) (h‚ÇÅ : Function.LeftInverse f‚ÇÇ f‚ÇÅ)
    (h‚ÇÇ : Function.RightInverse f‚ÇÇ f‚ÇÅ) : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ :=
  { f‚ÇÅ with
    continuous_toFun := f‚ÇÅ.continuous
    invFun := f‚ÇÇ
    continuous_invFun := f‚ÇÇ.continuous
    left_inv := h‚ÇÅ
    right_inv := h‚ÇÇ }

@[simp]
theorem equivOfInverse_apply (f‚ÇÅ : M‚ÇÅ ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (f‚ÇÇ h‚ÇÅ h‚ÇÇ x) :
    equivOfInverse f‚ÇÅ f‚ÇÇ h‚ÇÅ h‚ÇÇ x = f‚ÇÅ x :=
  rfl

@[simp]
theorem symm_equivOfInverse (f‚ÇÅ : M‚ÇÅ ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (f‚ÇÇ h‚ÇÅ h‚ÇÇ) :
    (equivOfInverse f‚ÇÅ f‚ÇÇ h‚ÇÅ h‚ÇÇ).symm = equivOfInverse f‚ÇÇ f‚ÇÅ h‚ÇÇ h‚ÇÅ :=
  rfl

/-- Create a `ContinuousLinearEquiv` from two `ContinuousLinearMap`s that are
inverse of each other, in the `ContinuousLinearMap.comp` sense. See also `equivOfInverse`. -/
def equivOfInverse' (f‚ÇÅ : M‚ÇÅ ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (f‚ÇÇ : M‚ÇÇ ‚ÜíSL[œÉ‚ÇÇ‚ÇÅ] M‚ÇÅ)
    (h‚ÇÅ : f‚ÇÅ.comp f‚ÇÇ = .id R‚ÇÇ M‚ÇÇ) (h‚ÇÇ : f‚ÇÇ.comp f‚ÇÅ = .id R‚ÇÅ M‚ÇÅ) : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ :=
  equivOfInverse f‚ÇÅ f‚ÇÇ
    (fun x ‚Ü¶ by simpa using congr($(h‚ÇÇ) x)) (fun x ‚Ü¶ by simpa using congr($(h‚ÇÅ) x))

@[simp]
theorem equivOfInverse'_apply (f‚ÇÅ : M‚ÇÅ ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (f‚ÇÇ h‚ÇÅ h‚ÇÇ x) :
    equivOfInverse' f‚ÇÅ f‚ÇÇ h‚ÇÅ h‚ÇÇ x = f‚ÇÅ x :=
  rfl

/-- The inverse of `equivOfInverse'` is obtained by swapping the order of its parameters. -/
@[simp]
theorem symm_equivOfInverse' (f‚ÇÅ : M‚ÇÅ ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (f‚ÇÇ h‚ÇÅ h‚ÇÇ) :
    (equivOfInverse' f‚ÇÅ f‚ÇÇ h‚ÇÅ h‚ÇÇ).symm = equivOfInverse' f‚ÇÇ f‚ÇÅ h‚ÇÇ h‚ÇÅ :=
  rfl

theorem eq_comp_toContinuousLinearMap_symm (e‚ÇÅ‚ÇÇ : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) [RingHomCompTriple œÉ‚ÇÇ‚ÇÅ œÉ‚ÇÅ‚ÇÉ œÉ‚ÇÇ‚ÇÉ]
    (f : M‚ÇÇ ‚ÜíSL[œÉ‚ÇÇ‚ÇÉ] M‚ÇÉ) (g : M‚ÇÅ ‚ÜíSL[œÉ‚ÇÅ‚ÇÉ] M‚ÇÉ) :
    f = g.comp e‚ÇÅ‚ÇÇ.symm.toContinuousLinearMap ‚Üî f.comp e‚ÇÅ‚ÇÇ.toContinuousLinearMap = g := by
  aesop

variable (M‚ÇÅ)

/-- The continuous linear equivalences from `M` to itself form a group under composition. -/
instance automorphismGroup : Group (M‚ÇÅ ‚âÉL[R‚ÇÅ] M‚ÇÅ) where
  mul f g := g.trans f
  one := ContinuousLinearEquiv.refl R‚ÇÅ M‚ÇÅ
  inv f := f.symm
  mul_assoc f g h := rfl
  mul_one f := rfl
  one_mul f := rfl
  inv_mul_cancel f := ext <| funext fun _ ‚Ü¶ f.left_inv _

variable {M‚ÇÅ} {R‚ÇÑ : Type*} [Semiring R‚ÇÑ] [Module R‚ÇÑ M‚ÇÑ] {œÉ‚ÇÉ‚ÇÑ : R‚ÇÉ ‚Üí+* R‚ÇÑ} {œÉ‚ÇÑ‚ÇÉ : R‚ÇÑ ‚Üí+* R‚ÇÉ}
  [RingHomInvPair œÉ‚ÇÉ‚ÇÑ œÉ‚ÇÑ‚ÇÉ] [RingHomInvPair œÉ‚ÇÑ‚ÇÉ œÉ‚ÇÉ‚ÇÑ] {œÉ‚ÇÇ‚ÇÑ : R‚ÇÇ ‚Üí+* R‚ÇÑ} {œÉ‚ÇÅ‚ÇÑ : R‚ÇÅ ‚Üí+* R‚ÇÑ}
  [RingHomCompTriple œÉ‚ÇÇ‚ÇÅ œÉ‚ÇÅ‚ÇÑ œÉ‚ÇÇ‚ÇÑ] [RingHomCompTriple œÉ‚ÇÇ‚ÇÑ œÉ‚ÇÑ‚ÇÉ œÉ‚ÇÇ‚ÇÉ] [RingHomCompTriple œÉ‚ÇÅ‚ÇÉ œÉ‚ÇÉ‚ÇÑ œÉ‚ÇÅ‚ÇÑ]

/-- The continuous linear equivalence between `ULift M‚ÇÅ` and `M‚ÇÅ`.

This is a continuous version of `ULift.moduleEquiv`. -/
def ulift : ULift M‚ÇÅ ‚âÉL[R‚ÇÅ] M‚ÇÅ :=
  { ULift.moduleEquiv with
    continuous_toFun := continuous_uliftDown
    continuous_invFun := continuous_uliftUp }

/-- A pair of continuous (semi)linear equivalences generates an equivalence between the spaces of
continuous linear maps. See also `ContinuousLinearEquiv.arrowCongr`. -/
@[simps]
def arrowCongrEquiv (e‚ÇÅ‚ÇÇ : M‚ÇÅ ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (e‚ÇÑ‚ÇÉ : M‚ÇÑ ‚âÉSL[œÉ‚ÇÑ‚ÇÉ] M‚ÇÉ) :
    (M‚ÇÅ ‚ÜíSL[œÉ‚ÇÅ‚ÇÑ] M‚ÇÑ) ‚âÉ (M‚ÇÇ ‚ÜíSL[œÉ‚ÇÇ‚ÇÉ] M‚ÇÉ) where
  toFun f := (e‚ÇÑ‚ÇÉ : M‚ÇÑ ‚ÜíSL[œÉ‚ÇÑ‚ÇÉ] M‚ÇÉ).comp (f.comp (e‚ÇÅ‚ÇÇ.symm : M‚ÇÇ ‚ÜíSL[œÉ‚ÇÇ‚ÇÅ] M‚ÇÅ))
  invFun f := (e‚ÇÑ‚ÇÉ.symm : M‚ÇÉ ‚ÜíSL[œÉ‚ÇÉ‚ÇÑ] M‚ÇÑ).comp (f.comp (e‚ÇÅ‚ÇÇ : M‚ÇÅ ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ))
  left_inv f :=
    ContinuousLinearMap.ext fun x => by
      simp only [ContinuousLinearMap.comp_apply, symm_apply_apply, coe_coe]
  right_inv f :=
    ContinuousLinearMap.ext fun x => by
      simp only [ContinuousLinearMap.comp_apply, apply_symm_apply, coe_coe]

section Pi

/-- Combine a family of linear equivalences into a linear equivalence of `pi`-types.
This is `Equiv.piCongrLeft` as a `ContinuousLinearEquiv`.
-/
def piCongrLeft (R : Type*) [Semiring R] {Œπ Œπ' : Type*}
    (œÜ : Œπ ‚Üí Type*) [‚àÄ i, AddCommMonoid (œÜ i)] [‚àÄ i, Module R (œÜ i)]
    [‚àÄ i, TopologicalSpace (œÜ i)]
    (e : Œπ' ‚âÉ Œπ) : ((i' : Œπ') ‚Üí œÜ (e i')) ‚âÉL[R] (i : Œπ) ‚Üí œÜ i where
  __ := Homeomorph.piCongrLeft e
  __ := LinearEquiv.piCongrLeft R œÜ e

/-- The product over `S ‚äï T` of a family of topological modules
is isomorphic (topologically and algebraically) to the product of
(the product over `S`) and (the product over `T`).

This is `Equiv.sumPiEquivProdPi` as a `ContinuousLinearEquiv`.
-/
def sumPiEquivProdPi (R : Type*) [Semiring R] (S T : Type*)
    (A : S ‚äï T ‚Üí Type*) [‚àÄ st, AddCommMonoid (A st)] [‚àÄ st, Module R (A st)]
    [‚àÄ st, TopologicalSpace (A st)] :
    ((st : S ‚äï T) ‚Üí A st) ‚âÉL[R] ((s : S) ‚Üí A (Sum.inl s)) √ó ((t : T) ‚Üí A (Sum.inr t)) where
  __ := LinearEquiv.sumPiEquivProdPi R S T A
  __ := Homeomorph.sumPiEquivProdPi S T A

/-- The product `Œ† t : Œ±, f t` of a family of topological modules is isomorphic
(both topologically and algebraically) to the space `f ‚¨ù` when `Œ±` only contains `‚¨ù`.

This is `Equiv.piUnique` as a `ContinuousLinearEquiv`.
-/
@[simps! -fullyApplied]
def piUnique {Œ± : Type*} [Unique Œ±] (R : Type*) [Semiring R] (f : Œ± ‚Üí Type*)
    [‚àÄ x, AddCommMonoid (f x)] [‚àÄ x, Module R (f x)] [‚àÄ x, TopologicalSpace (f x)] :
    (Œ† t, f t) ‚âÉL[R] f default where
  __ := LinearEquiv.piUnique R f
  __ := Homeomorph.piUnique f

end Pi

section piCongrRight

variable {Œπ : Type*} {M : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (M i)] [‚àÄ i, AddCommMonoid (M i)]
  [‚àÄ i, Module R‚ÇÅ (M i)] {N : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (N i)] [‚àÄ i, AddCommMonoid (N i)]
  [‚àÄ i, Module R‚ÇÅ (N i)] (f : (i : Œπ) ‚Üí M i ‚âÉL[R‚ÇÅ] N i)

/-- Combine a family of continuous linear equivalences into a continuous linear equivalence of
pi-types. -/
def piCongrRight : ((i : Œπ) ‚Üí M i) ‚âÉL[R‚ÇÅ] (i : Œπ) ‚Üí N i :=
  { LinearEquiv.piCongrRight fun i ‚Ü¶ f i with
    continuous_toFun := by
      exact continuous_pi fun i ‚Ü¶ (f i).continuous_toFun.comp (continuous_apply i)
    continuous_invFun := by
      exact continuous_pi fun i => (f i).continuous_invFun.comp (continuous_apply i) }

@[simp]
theorem piCongrRight_apply (m : (i : Œπ) ‚Üí M i) (i : Œπ) :
    piCongrRight f m i = (f i) (m i) := rfl

@[simp]
theorem piCongrRight_symm_apply (n : (i : Œπ) ‚Üí N i) (i : Œπ) :
    (piCongrRight f).symm n i = (f i).symm (n i) := rfl

end piCongrRight

section DistribMulAction

variable {G : Type*} [Group G] [DistribMulAction G M‚ÇÅ] [ContinuousConstSMul G M‚ÇÅ]
  [SMulCommClass G R‚ÇÅ M‚ÇÅ]

/-- Scalar multiplication by a group element as a continuous linear equivalence. -/
@[simps! apply_toLinearEquiv apply_apply]
def smulLeft : G ‚Üí* M‚ÇÅ ‚âÉL[R‚ÇÅ] M‚ÇÅ where
  toFun g := ‚ü®DistribMulAction.toModuleAut _ _ g, continuous_const_smul _, continuous_const_smul _‚ü©
  map_mul' _ _ := toLinearEquiv_injective <| map_mul (DistribMulAction.toModuleAut _ _) _ _
  map_one' := toLinearEquiv_injective <| map_one <| DistribMulAction.toModuleAut _ _

end DistribMulAction

end AddCommMonoid

section Aut

/-!
### Automorphisms as continuous linear equivalences and as units of the ring of endomorphisms

The next theorems cover the identification between `M ‚âÉL[R] M` and the group of units of the ring
`M ‚ÜíL[R] M`.
-/

variable {R M : Type*} [Semiring R] [AddCommMonoid M] [Module R M] [TopologicalSpace M]

/-- An invertible continuous linear map `f` determines a continuous equivalence from `M` to itself.
-/
def ofUnit (f : (M ‚ÜíL[R] M)À£) : M ‚âÉL[R] M where
  toLinearEquiv :=
    { toFun := f.val
      map_add' := by simp
      map_smul' := by simp
      invFun := f.inv
      left_inv := fun x =>
        show (f.inv * f.val) x = x by
          rw [f.inv_val]
          simp
      right_inv := fun x =>
        show (f.val * f.inv) x = x by
          rw [f.val_inv]
          simp }
  continuous_toFun := f.val.continuous
  continuous_invFun := f.inv.continuous

/-- A continuous equivalence from `M` to itself determines an invertible continuous linear map. -/
def toUnit (f : M ‚âÉL[R] M) : (M ‚ÜíL[R] M)À£ where
  val := f
  inv := f.symm
  val_inv := by
    ext
    simp
  inv_val := by
    ext
    simp

variable (R M)

/-- The units of the algebra of continuous `R`-linear endomorphisms of `M` is multiplicatively
equivalent to the type of continuous linear equivalences between `M` and itself. -/
def unitsEquiv : (M ‚ÜíL[R] M)À£ ‚âÉ* M ‚âÉL[R] M where
  toFun := ofUnit
  invFun := toUnit
  map_mul' x y := by
    ext
    rfl

@[simp]
theorem unitsEquiv_apply (f : (M ‚ÜíL[R] M)À£) (x : M) : unitsEquiv R M f x = (f : M ‚ÜíL[R] M) x :=
  rfl

end Aut

section AutRing

/-!
### Units of a ring as linear automorphisms
-/

variable (R : Type*) [Semiring R] [TopologicalSpace R] [ContinuousMul R]

/-- Continuous linear equivalences `R ‚âÉL[R] R` are enumerated by `RÀ£`. -/
def unitsEquivAut : RÀ£ ‚âÉ R ‚âÉL[R] R where
  toFun u :=
    equivOfInverse (ContinuousLinearMap.smulRight (1 : R ‚ÜíL[R] R) ‚Üëu)
      (ContinuousLinearMap.smulRight (1 : R ‚ÜíL[R] R) ‚Üëu‚Åª¬π) (fun x => by simp) fun x => by simp
  invFun e :=
    ‚ü®e 1, e.symm 1, by rw [‚Üê smul_eq_mul, ‚Üê map_smul, smul_eq_mul, mul_one, symm_apply_apply], by
      rw [‚Üê smul_eq_mul, ‚Üê map_smul, smul_eq_mul, mul_one, apply_symm_apply]‚ü©
  left_inv u := Units.ext <| by simp
  right_inv e := ext‚ÇÅ <| by simp

variable {R}

@[simp]
theorem unitsEquivAut_apply (u : RÀ£) (x : R) : unitsEquivAut R u x = x * u :=
  rfl

@[simp]
theorem unitsEquivAut_apply_symm (u : RÀ£) (x : R) : (unitsEquivAut R u).symm x = x * ‚Üëu‚Åª¬π :=
  rfl

@[simp]
theorem unitsEquivAut_symm_apply (e : R ‚âÉL[R] R) : ‚Üë((unitsEquivAut R).symm e) = e 1 :=
  rfl

end AutRing

section Pi

variable (Œπ R M : Type*) [Unique Œπ] [Semiring R] [AddCommMonoid M] [Module R M]
  [TopologicalSpace M]

/-- If `Œπ` has a unique element, then `Œπ ‚Üí M` is continuously linear equivalent to `M`. -/
def funUnique : (Œπ ‚Üí M) ‚âÉL[R] M :=
  { Homeomorph.funUnique Œπ M with toLinearEquiv := LinearEquiv.funUnique Œπ R M }

variable {Œπ R M}

@[simp]
theorem coe_funUnique : ‚áë(funUnique Œπ R M) = Function.eval default :=
  rfl

@[simp]
theorem coe_funUnique_symm : ‚áë(funUnique Œπ R M).symm = Function.const Œπ :=
  rfl

variable (R M)

/-- Continuous linear equivalence between dependent functions `(i : Fin 2) ‚Üí M i` and `M 0 √ó M 1`.
-/
@[simps! -fullyApplied apply symm_apply]
def piFinTwo (M : Fin 2 ‚Üí Type*) [‚àÄ i, AddCommMonoid (M i)] [‚àÄ i, Module R (M i)]
    [‚àÄ i, TopologicalSpace (M i)] : ((i : _) ‚Üí M i) ‚âÉL[R] M 0 √ó M 1 :=
  { Homeomorph.piFinTwo M with toLinearEquiv := LinearEquiv.piFinTwo R M }

/-- Continuous linear equivalence between vectors in `M¬≤ = Fin 2 ‚Üí M` and `M √ó M`. -/
@[simps! -fullyApplied apply symm_apply]
def finTwoArrow : (Fin 2 ‚Üí M) ‚âÉL[R] M √ó M :=
  { piFinTwo R fun _ => M with toLinearEquiv := LinearEquiv.finTwoArrow R M }

section
variable {n : ‚Ñï} {R : Type*} {M : Fin n.succ ‚Üí Type*} {N : Type*}
variable [Semiring R]
variable [‚àÄ i, AddCommMonoid (M i)] [‚àÄ i, Module R (M i)] [‚àÄ i, TopologicalSpace (M i)]

variable (R M) in
/-- `Fin.consEquiv` as a continuous linear equivalence. -/
@[simps!]
def _root_.Fin.consEquivL : (M 0 √ó Œ† i, M (Fin.succ i)) ‚âÉL[R] (Œ† i, M i) where
  __ := Fin.consLinearEquiv R M
  continuous_toFun := continuous_id.fst.finCons continuous_id.snd
  continuous_invFun := .prodMk (continuous_apply 0) (by continuity)

/-- `Fin.cons` in the codomain of continuous linear maps. -/
abbrev _root_.ContinuousLinearMap.finCons
    [AddCommMonoid N] [Module R N] [TopologicalSpace N]
    (f : N ‚ÜíL[R] M 0) (fs : N ‚ÜíL[R] Œ† i, M (Fin.succ i)) :
    N ‚ÜíL[R] Œ† i, M i :=
  Fin.consEquivL R M ‚àòL f.prod fs

end

end Pi

section AddCommGroup

variable {R : Type*} [Semiring R] {M : Type*} [TopologicalSpace M] [AddCommGroup M] {M‚ÇÇ : Type*}
  [TopologicalSpace M‚ÇÇ] [AddCommGroup M‚ÇÇ] {M‚ÇÉ : Type*} [TopologicalSpace M‚ÇÉ] [AddCommGroup M‚ÇÉ]
  {M‚ÇÑ : Type*} [TopologicalSpace M‚ÇÑ] [AddCommGroup M‚ÇÑ] [Module R M] [Module R M‚ÇÇ] [Module R M‚ÇÉ]
  [Module R M‚ÇÑ]

variable [IsTopologicalAddGroup M‚ÇÑ]

/-- Equivalence given by a block lower diagonal matrix. `e` and `e'` are diagonal square blocks,
  and `f` is a rectangular block below the diagonal. -/
def skewProd (e : M ‚âÉL[R] M‚ÇÇ) (e' : M‚ÇÉ ‚âÉL[R] M‚ÇÑ) (f : M ‚ÜíL[R] M‚ÇÑ) : (M √ó M‚ÇÉ) ‚âÉL[R] M‚ÇÇ √ó M‚ÇÑ :=
  { e.toLinearEquiv.skewProd e'.toLinearEquiv ‚Üëf with
    continuous_toFun :=
      (e.continuous_toFun.comp continuous_fst).prodMk
        ((e'.continuous_toFun.comp continuous_snd).add <| f.continuous.comp continuous_fst)
    continuous_invFun :=
      (e.continuous_invFun.comp continuous_fst).prodMk
        (e'.continuous_invFun.comp <|
          continuous_snd.sub <| f.continuous.comp <| e.continuous_invFun.comp continuous_fst) }

@[simp]
theorem skewProd_apply (e : M ‚âÉL[R] M‚ÇÇ) (e' : M‚ÇÉ ‚âÉL[R] M‚ÇÑ) (f : M ‚ÜíL[R] M‚ÇÑ) (x) :
    e.skewProd e' f x = (e x.1, e' x.2 + f x.1) :=
  rfl

@[simp]
theorem skewProd_symm_apply (e : M ‚âÉL[R] M‚ÇÇ) (e' : M‚ÇÉ ‚âÉL[R] M‚ÇÑ) (f : M ‚ÜíL[R] M‚ÇÑ) (x) :
    (e.skewProd e' f).symm x = (e.symm x.1, e'.symm (x.2 - f (e.symm x.1))) :=
  rfl

variable (R) in
/-- The negation map as a continuous linear equivalence. -/
def neg [ContinuousNeg M] :
    M ‚âÉL[R] M :=
  { LinearEquiv.neg R with
    continuous_toFun := continuous_neg
    continuous_invFun := continuous_neg }

@[simp]
theorem coe_neg [ContinuousNeg M] :
    (neg R : M ‚Üí M) = -id := rfl

@[simp]
theorem neg_apply [ContinuousNeg M] (x : M) :
    neg R x = -x := by simp

@[simp]
theorem symm_neg [ContinuousNeg M] :
    (neg R : M ‚âÉL[R] M).symm = neg R := rfl

end AddCommGroup

section Ring

variable {R : Type*} [Ring R] {R‚ÇÇ : Type*} [Ring R‚ÇÇ] {M : Type*} [TopologicalSpace M]
  [AddCommGroup M] [Module R M] {M‚ÇÇ : Type*} [TopologicalSpace M‚ÇÇ] [AddCommGroup M‚ÇÇ] [Module R‚ÇÇ M‚ÇÇ]

variable {œÉ‚ÇÅ‚ÇÇ : R ‚Üí+* R‚ÇÇ} {œÉ‚ÇÇ‚ÇÅ : R‚ÇÇ ‚Üí+* R} [RingHomInvPair œÉ‚ÇÅ‚ÇÇ œÉ‚ÇÇ‚ÇÅ] [RingHomInvPair œÉ‚ÇÇ‚ÇÅ œÉ‚ÇÅ‚ÇÇ]

theorem map_sub (e : M ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (x y : M) : e (x - y) = e x - e y :=
  (e : M ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ).map_sub x y

theorem map_neg (e : M ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (x : M) : e (-x) = -e x :=
  (e : M ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ).map_neg x

variable [Module R M‚ÇÇ] [IsTopologicalAddGroup M]

/-- A pair of continuous linear maps such that `f‚ÇÅ ‚àò f‚ÇÇ = id` generates a continuous
linear equivalence `e` between `M` and `M‚ÇÇ √ó f‚ÇÅ.ker` such that `(e x).2 = x` for `x ‚àà f‚ÇÅ.ker`,
`(e x).1 = f‚ÇÅ x`, and `(e (f‚ÇÇ y)).2 = 0`. The map is given by `e x = (f‚ÇÅ x, x - f‚ÇÇ (f‚ÇÅ x))`. -/
def equivOfRightInverse (f‚ÇÅ : M ‚ÜíL[R] M‚ÇÇ) (f‚ÇÇ : M‚ÇÇ ‚ÜíL[R] M) (h : Function.RightInverse f‚ÇÇ f‚ÇÅ) :
    M ‚âÉL[R] M‚ÇÇ √ó f‚ÇÅ.ker :=
  equivOfInverse (f‚ÇÅ.prod (f‚ÇÅ.projKerOfRightInverse f‚ÇÇ h)) (f‚ÇÇ.coprod f‚ÇÅ.ker.subtypeL)
    (fun x => by simp) fun ‚ü®x, y‚ü© => by simp [h x]

@[simp]
theorem fst_equivOfRightInverse (f‚ÇÅ : M ‚ÜíL[R] M‚ÇÇ) (f‚ÇÇ : M‚ÇÇ ‚ÜíL[R] M)
    (h : Function.RightInverse f‚ÇÇ f‚ÇÅ) (x : M) : (equivOfRightInverse f‚ÇÅ f‚ÇÇ h x).1 = f‚ÇÅ x :=
  rfl

@[simp]
theorem snd_equivOfRightInverse (f‚ÇÅ : M ‚ÜíL[R] M‚ÇÇ) (f‚ÇÇ : M‚ÇÇ ‚ÜíL[R] M)
    (h : Function.RightInverse f‚ÇÇ f‚ÇÅ) (x : M) :
    ((equivOfRightInverse f‚ÇÅ f‚ÇÇ h x).2 : M) = x - f‚ÇÇ (f‚ÇÅ x) :=
  rfl

@[simp]
theorem equivOfRightInverse_symm_apply (f‚ÇÅ : M ‚ÜíL[R] M‚ÇÇ) (f‚ÇÇ : M‚ÇÇ ‚ÜíL[R] M)
    (h : Function.RightInverse f‚ÇÇ f‚ÇÅ) (y : M‚ÇÇ √ó f‚ÇÅ.ker) :
    (equivOfRightInverse f‚ÇÅ f‚ÇÇ h).symm y = f‚ÇÇ y.1 + y.2 :=
  rfl

end Ring

end ContinuousLinearEquiv

namespace ContinuousLinearMap

variable {R : Type*} {M M‚ÇÇ M‚ÇÉ : Type*}
  [TopologicalSpace M] [TopologicalSpace M‚ÇÇ] [TopologicalSpace M‚ÇÉ]

variable [Semiring R]
  [AddCommMonoid M] [Module R M]
  [AddCommMonoid M‚ÇÇ] [Module R M‚ÇÇ]
  [AddCommMonoid M‚ÇÉ] [Module R M‚ÇÉ]

/-- A continuous linear map is invertible if it is the forward direction of a continuous linear
equivalence. -/
def IsInvertible (f : M ‚ÜíL[R] M‚ÇÇ) : Prop :=
  ‚àÉ (A : M ‚âÉL[R] M‚ÇÇ), A = f

open Classical in
/-- Introduce a function `inverse` from `M ‚ÜíL[R] M‚ÇÇ` to `M‚ÇÇ ‚ÜíL[R] M`, which sends `f` to `f.symm` if
`f` is a continuous linear equivalence and to `0` otherwise.  This definition is somewhat ad hoc,
but one needs a fully (rather than partially) defined inverse function for some purposes, including
for calculus. -/
noncomputable def inverse : (M ‚ÜíL[R] M‚ÇÇ) ‚Üí M‚ÇÇ ‚ÜíL[R] M := fun f =>
  if h : f.IsInvertible then ((Classical.choose h).symm : M‚ÇÇ ‚ÜíL[R] M) else 0

@[simp] lemma isInvertible_equiv {f : M ‚âÉL[R] M‚ÇÇ} : IsInvertible (f : M ‚ÜíL[R] M‚ÇÇ) := ‚ü®f, rfl‚ü©

/-- By definition, if `f` is invertible then `inverse f = f.symm`. -/
@[simp]
theorem inverse_equiv (e : M ‚âÉL[R] M‚ÇÇ) : inverse (e : M ‚ÜíL[R] M‚ÇÇ) = e.symm := by
  simp [inverse]

/-- By definition, if `f` is not invertible then `inverse f = 0`. -/
@[simp] lemma inverse_of_not_isInvertible
    {f : M ‚ÜíL[R] M‚ÇÇ} (hf : ¬¨ f.IsInvertible) : f.inverse = 0 :=
  dif_neg hf

@[simp]
theorem isInvertible_zero_iff :
    IsInvertible (0 : M ‚ÜíL[R] M‚ÇÇ) ‚Üî Subsingleton M ‚àß Subsingleton M‚ÇÇ := by
  refine ‚ü®fun ‚ü®e, he‚ü© ‚Ü¶ ?_, ?_‚ü©
  ¬∑ have A : Subsingleton M := by
      refine ‚ü®fun x y ‚Ü¶ e.injective ?_‚ü©
      simp [he, ‚Üê ContinuousLinearEquiv.coe_coe]
    exact ‚ü®A, e.toEquiv.symm.subsingleton‚ü©
  ¬∑ rintro ‚ü®hM, hM‚ÇÇ‚ü©
    let e : M ‚âÉL[R] M‚ÇÇ :=
    { toFun := 0
      invFun := 0
      left_inv x := Subsingleton.elim _ _
      right_inv x := Subsingleton.elim _ _
      map_add' x y := Subsingleton.elim _ _
      map_smul' c x := Subsingleton.elim _ _ }
    refine ‚ü®e, ?_‚ü©
    ext x
    exact Subsingleton.elim _ _

@[simp] theorem inverse_zero : inverse (0 : M ‚ÜíL[R] M‚ÇÇ) = 0 := by
  by_cases h : IsInvertible (0 : M ‚ÜíL[R] M‚ÇÇ)
  ¬∑ rcases isInvertible_zero_iff.1 h with ‚ü®hM, hM‚ÇÇ‚ü©
    ext x
    exact Subsingleton.elim _ _
  ¬∑ exact inverse_of_not_isInvertible h

lemma IsInvertible.comp {g : M‚ÇÇ ‚ÜíL[R] M‚ÇÉ} {f : M ‚ÜíL[R] M‚ÇÇ}
    (hg : g.IsInvertible) (hf : f.IsInvertible) : (g ‚àòL f).IsInvertible := by
  rcases hg with ‚ü®N, rfl‚ü©
  rcases hf with ‚ü®M, rfl‚ü©
  exact ‚ü®M.trans N, rfl‚ü©

lemma IsInvertible.of_inverse {f : M ‚ÜíL[R] M‚ÇÇ} {g : M‚ÇÇ ‚ÜíL[R] M}
    (hf : f ‚àòL g = .id R M‚ÇÇ) (hg : g ‚àòL f = .id R M) :
    f.IsInvertible :=
  ‚ü®ContinuousLinearEquiv.equivOfInverse' _ _ hf hg, rfl‚ü©

lemma inverse_eq {f : M ‚ÜíL[R] M‚ÇÇ} {g : M‚ÇÇ ‚ÜíL[R] M}
    (hf : f ‚àòL g = .id R M‚ÇÇ) (hg : g ‚àòL f = .id R M) :
    f.inverse = g := by
  have : f = ContinuousLinearEquiv.equivOfInverse' f g hf hg := rfl
  rw [this, inverse_equiv]
  rfl

lemma IsInvertible.inverse_apply_eq {f : M ‚ÜíL[R] M‚ÇÇ} {x : M} {y : M‚ÇÇ} (hf : f.IsInvertible) :
    f.inverse y = x ‚Üî y = f x := by
  rcases hf with ‚ü®M, rfl‚ü©
  simp only [inverse_equiv, ContinuousLinearEquiv.coe_coe]
  exact ContinuousLinearEquiv.symm_apply_eq M

@[simp] lemma isInvertible_equiv_comp {e : M‚ÇÇ ‚âÉL[R] M‚ÇÉ} {f : M ‚ÜíL[R] M‚ÇÇ} :
    ((e : M‚ÇÇ ‚ÜíL[R] M‚ÇÉ) ‚àòL f).IsInvertible ‚Üî f.IsInvertible := by
  constructor
  ¬∑ rintro ‚ü®A, hA‚ü©
    have : f = e.symm ‚àòL ((e : M‚ÇÇ ‚ÜíL[R] M‚ÇÉ) ‚àòL f) := by ext; simp
    rw [this, ‚Üê hA]
    simp
  ¬∑ rintro ‚ü®M, rfl‚ü©
    simp

@[simp] lemma isInvertible_comp_equiv {e : M‚ÇÉ ‚âÉL[R] M} {f : M ‚ÜíL[R] M‚ÇÇ} :
    (f ‚àòL (e : M‚ÇÉ ‚ÜíL[R] M)).IsInvertible ‚Üî f.IsInvertible := by
  constructor
  ¬∑ rintro ‚ü®A, hA‚ü©
    have : f = (f ‚àòL (e : M‚ÇÉ ‚ÜíL[R] M)) ‚àòL e.symm := by ext; simp
    rw [this, ‚Üê hA]
    simp
  ¬∑ rintro ‚ü®M, rfl‚ü©
    simp

@[simp] lemma inverse_equiv_comp {e : M‚ÇÇ ‚âÉL[R] M‚ÇÉ} {f : M ‚ÜíL[R] M‚ÇÇ} :
    (e ‚àòL f).inverse = f.inverse ‚àòL (e.symm : M‚ÇÉ ‚ÜíL[R] M‚ÇÇ) := by
  by_cases hf : f.IsInvertible
  ¬∑ rcases hf with ‚ü®A, rfl‚ü©
    simp only [ContinuousLinearEquiv.comp_coe, inverse_equiv, ContinuousLinearEquiv.coe_inj]
    rfl
  ¬∑ rw [inverse_of_not_isInvertible (by simp [hf]), inverse_of_not_isInvertible hf, zero_comp]

@[simp] lemma inverse_comp_equiv {e : M‚ÇÉ ‚âÉL[R] M} {f : M ‚ÜíL[R] M‚ÇÇ} :
    (f ‚àòL e).inverse = (e.symm : M ‚ÜíL[R] M‚ÇÉ) ‚àòL f.inverse := by
  by_cases hf : f.IsInvertible
  ¬∑ rcases hf with ‚ü®A, rfl‚ü©
    simp only [ContinuousLinearEquiv.comp_coe, inverse_equiv, ContinuousLinearEquiv.coe_inj]
    rfl
  ¬∑ rw [inverse_of_not_isInvertible (by simp [hf]), inverse_of_not_isInvertible hf, comp_zero]

lemma IsInvertible.inverse_comp_of_left {g : M‚ÇÇ ‚ÜíL[R] M‚ÇÉ} {f : M ‚ÜíL[R] M‚ÇÇ}
    (hg : g.IsInvertible) : (g ‚àòL f).inverse = f.inverse ‚àòL g.inverse := by
  rcases hg with ‚ü®N, rfl‚ü©
  simp

lemma IsInvertible.inverse_comp_apply_of_left {g : M‚ÇÇ ‚ÜíL[R] M‚ÇÉ} {f : M ‚ÜíL[R] M‚ÇÇ} {v : M‚ÇÉ}
    (hg : g.IsInvertible) : (g ‚àòL f).inverse v = f.inverse (g.inverse v) := by
  simp only [hg.inverse_comp_of_left, coe_comp', Function.comp_apply]

lemma IsInvertible.inverse_comp_of_right {g : M‚ÇÇ ‚ÜíL[R] M‚ÇÉ} {f : M ‚ÜíL[R] M‚ÇÇ}
    (hf : f.IsInvertible) : (g ‚àòL f).inverse = f.inverse ‚àòL g.inverse := by
  rcases hf with ‚ü®M, rfl‚ü©
  simp

lemma IsInvertible.inverse_comp_apply_of_right {g : M‚ÇÇ ‚ÜíL[R] M‚ÇÉ} {f : M ‚ÜíL[R] M‚ÇÇ} {v : M‚ÇÉ}
    (hf : f.IsInvertible) : (g ‚àòL f).inverse v = f.inverse (g.inverse v) := by
  simp only [hf.inverse_comp_of_right, coe_comp', Function.comp_apply]

@[simp]
theorem ringInverse_equiv (e : M ‚âÉL[R] M) : Ring.inverse ‚Üëe = inverse (e : M ‚ÜíL[R] M) := by
  suffices Ring.inverse ((ContinuousLinearEquiv.unitsEquiv _ _).symm e : M ‚ÜíL[R] M) = inverse ‚Üëe by
    convert this
  simp
  rfl

/-- The function `ContinuousLinearEquiv.inverse` can be written in terms of `Ring.inverse` for the
ring of self-maps of the domain. -/
theorem inverse_eq_ringInverse (e : M ‚âÉL[R] M‚ÇÇ) (f : M ‚ÜíL[R] M‚ÇÇ) :
    inverse f = Ring.inverse ((e.symm : M‚ÇÇ ‚ÜíL[R] M).comp f) ‚àòL e.symm := by
  by_cases h‚ÇÅ : f.IsInvertible
  ¬∑ obtain ‚ü®e', he'‚ü© := h‚ÇÅ
    rw [‚Üê he']
    change _ = Ring.inverse (e'.trans e.symm : M ‚ÜíL[R] M) ‚àòL (e.symm : M‚ÇÇ ‚ÜíL[R] M)
    ext
    simp
  ¬∑ suffices ¬¨IsUnit ((e.symm : M‚ÇÇ ‚ÜíL[R] M).comp f) by simp [this, h‚ÇÅ]
    contrapose! h‚ÇÅ
    rcases h‚ÇÅ with ‚ü®F, hF‚ü©
    use (ContinuousLinearEquiv.unitsEquiv _ _ F).trans e
    ext
    dsimp
    rw [hF]
    simp

theorem ringInverse_eq_inverse : Ring.inverse = inverse (R := R) (M := M) := by
  ext
  simp [inverse_eq_ringInverse (ContinuousLinearEquiv.refl R M)]

@[simp] theorem inverse_id : (ContinuousLinearMap.id R M).inverse = .id R M := by
  rw [‚Üê ringInverse_eq_inverse]
  exact Ring.inverse_one _

namespace IsInvertible

variable {f : M ‚ÜíL[R] M‚ÇÇ}

@[simp]
theorem self_comp_inverse (hf : f.IsInvertible) : f ‚àòL f.inverse = .id _ _ := by
  rcases hf with ‚ü®e, rfl‚ü©
  simp

@[simp]
theorem self_apply_inverse (hf : f.IsInvertible) (y : M‚ÇÇ) : f (f.inverse y) = y := by
  rcases hf with ‚ü®e, rfl‚ü©
  simp

@[simp]
theorem inverse_comp_self (hf : f.IsInvertible) : f.inverse ‚àòL f = .id _ _ := by
  rcases hf with ‚ü®e, rfl‚ü©
  simp

@[simp]
theorem inverse_apply_self (hf : f.IsInvertible) (y : M) : f.inverse (f y) = y := by
  rcases hf with ‚ü®e, rfl‚ü©
  simp

protected theorem bijective (hf : f.IsInvertible) : Function.Bijective f := by
  rcases hf with ‚ü®e, rfl‚ü©
  simp [ContinuousLinearEquiv.bijective]

protected theorem injective (hf : f.IsInvertible) : Function.Injective f :=
  hf.bijective.injective

protected theorem surjective (hf : f.IsInvertible) : Function.Surjective f :=
  hf.bijective.surjective

protected theorem inverse (hf : f.IsInvertible) : f.inverse.IsInvertible := by
  rcases hf with ‚ü®e, rfl‚ü©
  simp

@[simp]
protected theorem inverse_inverse (hf : f.IsInvertible) : f.inverse.inverse = f := by
  rcases hf with ‚ü®e, rfl‚ü©
  simp

protected theorem of_isInvertible_inverse (hf : f.inverse.IsInvertible) : f.IsInvertible := by
  by_contra H
  obtain ‚ü®_, _‚ü© : Subsingleton M‚ÇÇ ‚àß Subsingleton M := by simpa [inverse, H] using hf
  simp_all [Subsingleton.elim f 0]

@[simp]
theorem _root_.ContinuousLinearMap.isInvertible_inverse_iff :
    f.inverse.IsInvertible ‚Üî f.IsInvertible :=
  ‚ü®.of_isInvertible_inverse, .inverse‚ü©

end IsInvertible

/-- Composition of a map on a product with the exchange of the product factors -/
theorem coprod_comp_prodComm [ContinuousAdd M] (f : M‚ÇÇ ‚ÜíL[R] M) (g : M‚ÇÉ ‚ÜíL[R] M) :
    f.coprod g ‚àòL ContinuousLinearEquiv.prodComm R M‚ÇÉ M‚ÇÇ = g.coprod f := by
  ext <;> simp

end ContinuousLinearMap

-- Restricting a continuous linear equivalence to a map between submodules.
section map

namespace ContinuousLinearEquiv

variable {R R‚ÇÇ M M‚ÇÇ : Type*} [Semiring R] [Semiring R‚ÇÇ] [AddCommMonoid M] [TopologicalSpace M]
  [AddCommMonoid M‚ÇÇ] [TopologicalSpace M‚ÇÇ]
  {module_M : Module R M} {module_M‚ÇÇ : Module R‚ÇÇ M‚ÇÇ} {œÉ‚ÇÅ‚ÇÇ : R ‚Üí+* R‚ÇÇ} {œÉ‚ÇÇ‚ÇÅ : R‚ÇÇ ‚Üí+* R}
  {re‚ÇÅ‚ÇÇ : RingHomInvPair œÉ‚ÇÅ‚ÇÇ œÉ‚ÇÇ‚ÇÅ} {re‚ÇÇ‚ÇÅ : RingHomInvPair œÉ‚ÇÇ‚ÇÅ œÉ‚ÇÅ‚ÇÇ}

/-- Continuous linear equivalence between two equal submodules:
this is `LinearEquiv.ofEq` as a continuous linear equivalence -/
def ofEq (p q : Submodule R M) (h : p = q) : p ‚âÉL[R] q where
  toLinearEquiv := LinearEquiv.ofEq _ _ h
  continuous_toFun := by
    have h' : (fun x ‚Ü¶ x ‚àà p) = (fun x ‚Ü¶ x ‚àà q) := by simp [h]
    exact (Homeomorph.ofEqSubtypes h').continuous
  continuous_invFun := by
    have h' : (fun x ‚Ü¶ x ‚àà p) = (fun x ‚Ü¶ x ‚àà q) := by simp [h]
    exact (Homeomorph.ofEqSubtypes h').symm.continuous

/--
A continuous linear equivalence of two modules restricts to a continuous linear equivalence
from any submodule `p` of the domain onto the image of that submodule.

This is the continuous linear version of `LinearEquiv.submoduleMap`.
This is `ContinuousLinearEquiv.ofSubmodule'` but with map on the right instead of comap on the left.
-/
def submoduleMap (e : M ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (p : Submodule R M) :
    p ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] Submodule.map (e : M ‚Üí‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) p where
  __ := LinearEquiv.submoduleMap e.toLinearEquiv p
  continuous_toFun := map_continuous ((e.toContinuousLinearMap.comp p.subtypeL).codRestrict _ _)
  continuous_invFun := (map_continuous e.symm).restrict fun x hx ‚Ü¶
    ((LinearEquiv.submoduleMap e.toLinearEquiv p).symm ‚ü®x, hx‚ü©).2

@[simp]
lemma submoduleMap_apply (e : M ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (p : Submodule R M) (x : p) :
    e.submoduleMap p x = e x := by
  rfl

@[simp]
lemma submoduleMap_symm_apply (e : M ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (p : Submodule R M)
    (x : p.map (e : M ‚Üí‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ)) :
    (e.submoduleMap p).symm x = e.symm x := by
  rfl

/-- A continuous linear equivalence which maps a submodule of one module onto another,
restricts to a continuous linear equivalence of the two submodules.
This is `LinearEquiv.ofSubmodules` as a continuous linear equivalence. -/
def ofSubmodules (e : M ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ)
    (p : Submodule R M) (q : Submodule R‚ÇÇ M‚ÇÇ) (h : p.map (e : M ‚Üí‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) = q) : p ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] q :=
  (e.submoduleMap p).trans (.ofEq _ _ h)

@[simp]
theorem ofSubmodules_apply (e : M ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) {p : Submodule R M} {q : Submodule R‚ÇÇ M‚ÇÇ}
    (h : p.map (e : M ‚Üí‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) = q) (x : p) :
    e.ofSubmodules p q h x = e x :=
  rfl

@[simp]
theorem ofSubmodules_symm_apply (e : M ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) {p : Submodule R M} {q : Submodule R‚ÇÇ M‚ÇÇ}
    (h : p.map (e : M ‚Üí‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) = q) (x : q) : (e.ofSubmodules p q h).symm x = e.symm x :=
  rfl

/-- A continuous linear equivalence of two modules restricts to a continuous linear equivalence
from the preimage of any submodule to that submodule.
This is `ContinuousLinearEquiv.ofSubmodule` but with `comap` on the left
instead of `map` on the right. -/
def ofSubmodule' (f : M ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (U : Submodule R‚ÇÇ M‚ÇÇ) :
    U.comap (f : M ‚Üí‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] U :=
  f.symm.ofSubmodules _ _ (U.map_equiv_eq_comap_symm f.toLinearEquiv.symm) |>.symm

theorem ofSubmodule'_toContinuousLinearMap (f : M ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (U : Submodule R‚ÇÇ M‚ÇÇ) :
    (f.ofSubmodule' U).toContinuousLinearMap =
      (f.toContinuousLinearMap.comp ((U.comap f.toLinearMap).subtypeL)).codRestrict U
        ((fun ‚ü®x, hx‚ü© ‚Ü¶ by simpa [Submodule.mem_comap])) := by
  rfl

@[simp]
theorem ofSubmodule'_apply (f : M ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (U : Submodule R‚ÇÇ M‚ÇÇ)
    (x : U.comap (f : M ‚Üí‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ)) :
    (f.ofSubmodule' U x : M‚ÇÇ) = f (x : M) :=
  rfl

@[simp]
theorem ofSubmodule'_symm_apply (f : M ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ) (U : Submodule R‚ÇÇ M‚ÇÇ) (x : U) :
    ((f.ofSubmodule' U).symm x : M) = f.symm (x : M‚ÇÇ) := rfl

end ContinuousLinearEquiv

end map

namespace Submodule

variable {R : Type*} [Ring R] {M : Type*} [TopologicalSpace M] [AddCommGroup M] [Module R M]

open ContinuousLinearMap

/-- If `p` is a closed complemented submodule,
then there exists a submodule `q` and a continuous linear equivalence `M ‚âÉL[R] (p √ó q)` such that
`e (x : p) = (x, 0)`, `e (y : q) = (0, y)`, and `e.symm x = x.1 + x.2`.

In fact, the properties of `e` imply the properties of `e.symm` and vice versa,
but we provide both for convenience. -/
lemma ClosedComplemented.exists_submodule_equiv_prod [IsTopologicalAddGroup M]
    {p : Submodule R M} (hp : p.ClosedComplemented) :
    ‚àÉ (q : Submodule R M) (e : M ‚âÉL[R] (p √ó q)),
      (‚àÄ x : p, e x = (x, 0)) ‚àß (‚àÄ y : q, e y = (0, y)) ‚àß (‚àÄ x, e.symm x = x.1 + x.2) :=
  let ‚ü®f, hf‚ü© := hp
  ‚ü®f.ker, .equivOfRightInverse f p.subtypeL hf,
    fun _ ‚Ü¶ by ext <;> simp [hf], fun _ ‚Ü¶ by ext <;> simp, fun _ ‚Ü¶ rfl‚ü©

end Submodule

namespace MulOpposite

variable (R : Type*) [Semiring R] [œÑR : TopologicalSpace R] [IsTopologicalSemiring R]
  {M : Type*} [AddCommMonoid M] [Module R M] [TopologicalSpace M] [ContinuousSMul R M]

/-- The function `op` is a continuous linear equivalence. -/
@[simps!]
def opContinuousLinearEquiv : M ‚âÉL[R] M·µê·µí·µñ where
  __ := MulOpposite.opLinearEquiv R

end MulOpposite

end
