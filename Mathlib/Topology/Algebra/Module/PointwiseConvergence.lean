/-
Copyright (c) 2024 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
module

public import Mathlib.Topology.Algebra.Module.StrongTopology
public import Mathlib.Topology.Algebra.Module.WeakDual

/-!
# Topology of pointwise convergence on continuous linear maps

## Main definitions

* `PointwiseConvergenceCLM`: Type synonym of `E â†’SL[Ïƒ] F` equipped with the uniform convergence
topology on finite sets.
* `PointwiseConvergenceCLM.evalCLM`: The evaluation map `(f : E â†’SLâ‚šâ‚œ[Ïƒ] F) â†¦ f a` for fixed `a : E`
as a continuous linear map.
* `ContinuousLinearMap.toPointwiseConvergenceCLM`: The canonical map from `E â†’SL[Ïƒ] F` to
`E â†’SLâ‚šâ‚œ[Ïƒ] F` as a continuous linear map. This is the statement that bounded convergence is
stronger than pointwise convergence.
* `PointwiseConvergenceCLM.equivWeakDual`: The continuous equivalence between `E â†’Lâ‚šâ‚œ[ğ•œ] ğ•œ` and
`WeakDual ğ•œ E`.

## Main statements

* `PointwiseConvergenceCLM.tendsto_iff_forall_tendsto`: In the topology of pointwise convergence,
`a` converges to `aâ‚€` iff for every `x : E` the map `a Â· x` converges to `aâ‚€ x`.
* `PointwiseConvergenceCLM.continuous_of_continuous_eval`: A map to `g : Î± â†’ E â†’SLâ‚šâ‚œ[Ïƒ] F` is
continuous if for every `x : E` the evaluation `g Â· x` is continuous.

## Notation

* `E â†’SLâ‚šâ‚œ[Ïƒ] F` is space of continuous linear maps equipped with pointwise convergence topology.

-/

@[expose] public section

/-! ### Topology of pointwise convergence -/

variable {Î± Î¹ : Type*} [TopologicalSpace Î±]
variable {ğ•œ ğ•œâ‚ ğ•œâ‚‚ ğ•œâ‚ƒ : Type*} [NormedField ğ•œ] [NormedField ğ•œâ‚] [NormedField ğ•œâ‚‚] [NormedField ğ•œâ‚ƒ]
variable {Ïƒ : ğ•œâ‚ â†’+* ğ•œâ‚‚} {Ï„ : ğ•œâ‚‚ â†’+* ğ•œâ‚ƒ} {Ï : ğ•œâ‚ â†’+* ğ•œâ‚ƒ} [RingHomCompTriple Ïƒ Ï„ Ï]
variable {E F Fáµ¤ G : Type*} [AddCommGroup E] [TopologicalSpace E]
  [AddCommGroup F] [TopologicalSpace F] [IsTopologicalAddGroup F]
  [AddCommGroup G] [TopologicalSpace G] [IsTopologicalAddGroup G]
  [AddCommGroup Fáµ¤] [UniformSpace Fáµ¤] [IsUniformAddGroup Fáµ¤]
  [Module ğ•œ E] [Module ğ•œ F] [Module ğ•œ Fáµ¤] [Module ğ•œâ‚ E] [Module ğ•œâ‚‚ F] [Module ğ•œâ‚‚ Fáµ¤] [Module ğ•œâ‚ƒ G]

open Set Topology

variable (Ïƒ E F) in
/-- The space of continuous linear maps equipped with the topology of pointwise convergence,
sometimes also called the *strong operator topology*. We avoid this terminology since so many other
things share similar names, and using "pointwise convergence" in the name is more informative.

This topology is also known as the weak*-topology in the case that `Ïƒ = RingHom.id ğ•œ` and `F = ğ•œ` -/
abbrev PointwiseConvergenceCLM := UniformConvergenceCLM Ïƒ F {s : Set E | Finite s}

@[inherit_doc]
notation:25 E " â†’SLâ‚šâ‚œ[" Ïƒ "] " F => PointwiseConvergenceCLM Ïƒ E F

@[inherit_doc]
notation:25 E " â†’Lâ‚šâ‚œ[" R "] " F => PointwiseConvergenceCLM (RingHom.id R) E F

namespace PointwiseConvergenceCLM

instance [T2Space F] : T2Space (E â†’SLâ‚šâ‚œ[Ïƒ] F) :=
  UniformConvergenceCLM.t2Space _ _ _ Set.sUnion_finite_eq_univ

instance continuousEvalConst : ContinuousEvalConst (E â†’SLâ‚šâ‚œ[Ïƒ] F) E F :=
  UniformConvergenceCLM.continuousEvalConst _ _ _ Set.sUnion_finite_eq_univ

protected theorem hasBasis_nhds_zero_of_basis
    {Î¹ : Type*} {p : Î¹ â†’ Prop} {b : Î¹ â†’ Set F} (h : (ğ“ 0 : Filter F).HasBasis p b) :
    (ğ“ (0 : E â†’SLâ‚šâ‚œ[Ïƒ] F)).HasBasis (fun Si : Set E Ã— Î¹ => Finite Si.1 âˆ§ p Si.2)
      fun Si => { f : E â†’SLâ‚šâ‚œ[Ïƒ] F | âˆ€ x âˆˆ Si.1, f x âˆˆ b Si.2 } :=
  UniformConvergenceCLM.hasBasis_nhds_zero_of_basis Ïƒ F { S | Finite S }
    âŸ¨âˆ…, Set.finite_emptyâŸ© (directedOn_of_sup_mem fun _ _ => Set.Finite.union) h

protected theorem hasBasis_nhds_zero :
    (ğ“ (0 : E â†’SLâ‚šâ‚œ[Ïƒ] F)).HasBasis
      (fun SV : Set E Ã— Set F => Finite SV.1 âˆ§ SV.2 âˆˆ (ğ“ 0 : Filter F))
      fun SV => { f : E â†’SLâ‚šâ‚œ[Ïƒ] F | âˆ€ x âˆˆ SV.1, f x âˆˆ SV.2 } :=
  PointwiseConvergenceCLM.hasBasis_nhds_zero_of_basis (ğ“ 0).basis_sets

variable (Ïƒ E Fáµ¤) in
protected theorem isUniformEmbedding_coeFn :
    IsUniformEmbedding ((â†‘) : (E â†’SLâ‚šâ‚œ[Ïƒ] Fáµ¤) â†’ (E â†’ Fáµ¤)) :=
  (UniformOnFun.isUniformEmbedding_toFun_finite E Fáµ¤).comp
    (UniformConvergenceCLM.isUniformEmbedding_coeFn Ïƒ Fáµ¤ _)

variable (Ïƒ E F) in
protected theorem isEmbedding_coeFn : IsEmbedding ((â†‘) : (E â†’SLâ‚šâ‚œ[Ïƒ] F) â†’ (E â†’ F)) :=
  let _ : UniformSpace F := IsTopologicalAddGroup.rightUniformSpace F
  have _ : IsUniformAddGroup F := isUniformAddGroup_of_addCommGroup
  PointwiseConvergenceCLM.isUniformEmbedding_coeFn Ïƒ E F |>.isEmbedding

/-- In the topology of pointwise convergence, `a` converges to `aâ‚€` iff for every `x : E` the map
`a Â· x` converges to `aâ‚€ x`. -/
theorem tendsto_iff_forall_tendsto {p : Filter Î¹} {a : Î¹ â†’ E â†’SLâ‚šâ‚œ[Ïƒ] F} {aâ‚€ : E â†’SLâ‚šâ‚œ[Ïƒ] F} :
    Filter.Tendsto a p (ğ“ aâ‚€) â†” âˆ€ x : E, Filter.Tendsto (a Â· x) p (ğ“ (aâ‚€ x)) := by
  simp [(PointwiseConvergenceCLM.isEmbedding_coeFn Ïƒ E F).tendsto_nhds_iff, tendsto_pi_nhds]

variable (Ïƒ E F) in
/-- Coercion from `E â†’SLâ‚šâ‚œ[Ïƒ] F` to `E â†’â‚›â‚—[Ïƒ] F` as a `ğ•œâ‚‚`-linear map. -/
@[simps!]
def coeLMâ‚›â‚— [ContinuousConstSMul ğ•œâ‚‚ F] : (E â†’SLâ‚šâ‚œ[Ïƒ] F) â†’â‚—[ğ•œâ‚‚] E â†’â‚›â‚—[Ïƒ] F :=
  ContinuousLinearMap.coeLMâ‚›â‚— Ïƒ

variable (ğ•œ E F) in
/-- Coercion from `E â†’Lâ‚šâ‚œ[ğ•œ] F` to `E â†’â‚—[ğ•œ] F` as a `ğ•œ`-linear map. -/
@[simps!]
def coeLM [ContinuousConstSMul ğ•œ F] : (E â†’Lâ‚šâ‚œ[ğ•œ] F) â†’â‚—[ğ•œ] E â†’â‚—[ğ•œ] F := ContinuousLinearMap.coeLM ğ•œ

variable (Ïƒ F) in
/-- The evaluation map `(f : E â†’SLâ‚šâ‚œ[Ïƒ] F) â†¦ f a` for `a : E` as a continuous linear map. -/
@[simps!]
def evalCLM [ContinuousConstSMul ğ•œâ‚‚ F] (a : E) : (E â†’SLâ‚šâ‚œ[Ïƒ] F) â†’L[ğ•œâ‚‚] F where
  toLinearMap := (coeLMâ‚›â‚— Ïƒ E F).flip a
  cont := continuous_eval_const a

/-- A map to `E â†’SLâ‚šâ‚œ[Ïƒ] F` is continuous if for every `x : E` the evaluation `g Â· x` is
continuous. -/
theorem continuous_of_continuous_eval {g : Î± â†’ E â†’SLâ‚šâ‚œ[Ïƒ] F}
    (h : âˆ€ x, Continuous (g Â· x)) : Continuous g := by
  simp [(PointwiseConvergenceCLM.isEmbedding_coeFn Ïƒ E F).continuous_iff, continuous_pi_iff, h]

variable (G) in
/-- Pre-composition by a *fixed* continuous linear map as a continuous linear map for the pointwise
convergence topology. -/
@[simps! apply]
def precomp [IsTopologicalAddGroup G] [ContinuousConstSMul ğ•œâ‚ƒ G] (L : E â†’SL[Ïƒ] F) :
    (F â†’SLâ‚šâ‚œ[Ï„] G) â†’L[ğ•œâ‚ƒ] E â†’SLâ‚šâ‚œ[Ï] G where
  toFun f := f.comp L
  __ := ContinuousLinearMap.precomp_uniformConvergenceCLM G { (S : Set E) | Finite S }
    { (S : Set F) | Finite S } L (fun S hS â†¦ letI : Finite S := hS; Finite.Set.finite_image _ _)

variable (E) in
/-- Post-composition by a *fixed* continuous linear map as a continuous linear map for the pointwise
convergence topology. -/
@[simps! apply]
def postcomp [ContinuousConstSMul ğ•œâ‚‚ F] [ContinuousConstSMul ğ•œâ‚ƒ G] (L : F â†’SL[Ï„] G) :
    (E â†’SLâ‚šâ‚œ[Ïƒ] F) â†’SL[Ï„] E â†’SLâ‚šâ‚œ[Ï] G where
  toFun f := L.comp f
  __ := ContinuousLinearMap.postcomp_uniformConvergenceCLM { (S : Set E) | Finite S } L

variable (ğ•œâ‚‚ Ïƒ E F) in
/-- The topology of bounded convergence is stronger than the topology of pointwise convergence. -/
@[simps!]
def _root_.ContinuousLinearMap.toPointwiseConvergenceCLM [ContinuousSMul ğ•œâ‚ E]
    [ContinuousConstSMul ğ•œâ‚‚ F] : (E â†’SL[Ïƒ] F) â†’L[ğ•œâ‚‚] (E â†’SLâ‚šâ‚œ[Ïƒ] F) where
  __ := LinearMap.id
  cont := _root_.ContinuousLinearMap.toUniformConvergenceCLM_continuous Ïƒ F _
    (fun _ â†¦ Set.Finite.isVonNBounded)

variable (ğ•œ E) in
/-- The topology of pointwise convergence on `E â†’Lâ‚šâ‚œ[ğ•œ] ğ•œ` coincides with the weak-* topology. -/
@[simps!]
def equivWeakDual : (E â†’Lâ‚šâ‚œ[ğ•œ] ğ•œ) â‰ƒL[ğ•œ] WeakDual ğ•œ E where
  __ := LinearEquiv.refl ğ•œ (E â†’L[ğ•œ] ğ•œ)
  continuous_toFun :=
    WeakDual.continuous_of_continuous_eval (fun y â†¦ (evalCLM _ ğ•œ y).continuous)
  continuous_invFun := continuous_of_continuous_eval (WeakBilin.eval_continuous _)

end PointwiseConvergenceCLM
