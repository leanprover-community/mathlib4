/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, SÃ©bastien GouÃ«zel, Jean Lo, Yury Kudryashov, FrÃ©dÃ©ric Dupuis,
  Heather Macbeth
-/
import Mathlib.Algebra.Module.Opposite
import Mathlib.Topology.Algebra.Group.Quotient
import Mathlib.Topology.Algebra.Ring.Basic
import Mathlib.Topology.UniformSpace.UniformEmbedding
import Mathlib.LinearAlgebra.Finsupp.LinearCombination
import Mathlib.LinearAlgebra.Pi
import Mathlib.LinearAlgebra.Quotient.Defs

/-!
# Theory of topological modules

We use the class `ContinuousSMul` for topological (semi) modules and topological vector spaces.
-/

assert_not_exists Star.star

open LinearMap (ker range)
open Topology Filter Pointwise

universe u v w u'

section

variable {R : Type*} {M : Type*} [Ring R] [TopologicalSpace R] [TopologicalSpace M]
  [AddCommGroup M] [Module R M]

theorem ContinuousSMul.of_nhds_zero [IsTopologicalRing R] [IsTopologicalAddGroup M]
    (hmul : Tendsto (fun p : R Ã— M => p.1 â€¢ p.2) (ğ“ 0 Ã—Ë¢ ğ“ 0) (ğ“ 0))
    (hmulleft : âˆ€ m : M, Tendsto (fun a : R => a â€¢ m) (ğ“ 0) (ğ“ 0))
    (hmulright : âˆ€ a : R, Tendsto (fun m : M => a â€¢ m) (ğ“ 0) (ğ“ 0)) : ContinuousSMul R M where
  continuous_smul := by
    rw [â† nhds_prod_eq] at hmul
    refine continuous_of_continuousAt_zeroâ‚‚ (AddMonoidHom.smul : R â†’+ M â†’+ M) ?_ ?_ ?_ <;>
      simpa [ContinuousAt]

variable (R M) in
omit [TopologicalSpace R] in
/-- A topological module over a ring has continuous negation.

This cannot be an instance, because it would cause search for `[Module ?R M]` with unknown `R`. -/
theorem ContinuousNeg.of_continuousConstSMul [ContinuousConstSMul R M] : ContinuousNeg M where
  continuous_neg := by simpa using continuous_const_smul (T := M) (-1 : R)

end

section

variable {R : Type*} {M : Type*} [Ring R] [TopologicalSpace R] [TopologicalSpace M]
  [AddCommGroup M] [ContinuousAdd M] [Module R M] [ContinuousSMul R M]

/-- If `M` is a topological module over `R` and `0` is a limit of invertible elements of `R`, then
`âŠ¤` is the only submodule of `M` with a nonempty interior.
This is the case, e.g., if `R` is a nontrivially normed field. -/
theorem Submodule.eq_top_of_nonempty_interior' [NeBot (ğ“[{ x : R | IsUnit x }] 0)]
    (s : Submodule R M) (hs : (interior (s : Set M)).Nonempty) : s = âŠ¤ := by
  rcases hs with âŸ¨y, hyâŸ©
  refine Submodule.eq_top_iff'.2 fun x => ?_
  rw [mem_interior_iff_mem_nhds] at hy
  have : Tendsto (fun c : R => y + c â€¢ x) (ğ“[{ x : R | IsUnit x }] 0) (ğ“ (y + (0 : R) â€¢ x)) :=
    tendsto_const_nhds.add ((tendsto_nhdsWithin_of_tendsto_nhds tendsto_id).smul tendsto_const_nhds)
  rw [zero_smul, add_zero] at this
  obtain âŸ¨_, hu : y + _ â€¢ _ âˆˆ s, u, rflâŸ© :=
    nonempty_of_mem (inter_mem (Filter.mem_map.1 (this hy)) self_mem_nhdsWithin)
  have hy' : y âˆˆ â†‘s := mem_of_mem_nhds hy
  rwa [s.add_mem_iff_right hy', â† Units.smul_def, s.smul_mem_iff' u] at hu

variable (R M)

/-- Let `R` be a topological ring such that zero is not an isolated point (e.g., a nontrivially
normed field, see `NormedField.punctured_nhds_neBot`). Let `M` be a nontrivial module over `R`
such that `c â€¢ x = 0` implies `c = 0 âˆ¨ x = 0`. Then `M` has no isolated points. We formulate this
using `NeBot (ğ“[â‰ ] x)`.

This lemma is not an instance because Lean would need to find `[ContinuousSMul ?m_1 M]` with
unknown `?m_1`. We register this as an instance for `R = â„` in `Real.punctured_nhds_module_neBot`.
One can also use `haveI := Module.punctured_nhds_neBot R M` in a proof.
-/
theorem Module.punctured_nhds_neBot [Nontrivial M] [NeBot (ğ“[â‰ ] (0 : R))] [NoZeroSMulDivisors R M]
    (x : M) : NeBot (ğ“[â‰ ] x) := by
  rcases exists_ne (0 : M) with âŸ¨y, hyâŸ©
  suffices Tendsto (fun c : R => x + c â€¢ y) (ğ“[â‰ ] 0) (ğ“[â‰ ] x) from this.neBot
  refine Tendsto.inf ?_ (tendsto_principal_principal.2 <| ?_)
  Â· convert tendsto_const_nhds.add ((@tendsto_id R _).smul_const y)
    rw [zero_smul, add_zero]
  Â· intro c hc
    simpa [hy] using hc

end

section LatticeOps

variable {R Mâ‚ Mâ‚‚ : Type*} [SMul R Mâ‚] [SMul R Mâ‚‚] [u : TopologicalSpace R]
  {t : TopologicalSpace Mâ‚‚} [ContinuousSMul R Mâ‚‚]
  {F : Type*} [FunLike F Mâ‚ Mâ‚‚] [MulActionHomClass F R Mâ‚ Mâ‚‚] (f : F)

theorem continuousSMul_induced : @ContinuousSMul R Mâ‚ _ u (t.induced f) :=
  let _ : TopologicalSpace Mâ‚ := t.induced f
  IsInducing.continuousSMul âŸ¨rflâŸ© continuous_id (map_smul f _ _)

end LatticeOps

/-- The span of a separable subset with respect to a separable scalar ring is again separable. -/
lemma TopologicalSpace.IsSeparable.span {R M : Type*} [AddCommMonoid M] [Semiring R] [Module R M]
    [TopologicalSpace M] [TopologicalSpace R] [SeparableSpace R]
    [ContinuousAdd M] [ContinuousSMul R M] {s : Set M} (hs : IsSeparable s) :
    IsSeparable (Submodule.span R s : Set M) := by
  rw [Submodule.span_eq_iUnion_nat]
  refine .iUnion fun n â†¦ .image ?_ ?_
  Â· have : IsSeparable {f : Fin n â†’ R Ã— M | âˆ€ (i : Fin n), f i âˆˆ Set.univ Ã—Ë¢ s} := by
      apply isSeparable_pi (fun i â†¦ .prod (.of_separableSpace Set.univ) hs)
    rwa [Set.univ_prod] at this
  Â· apply continuous_finset_sum _ (fun i _ â†¦ ?_)
    exact (continuous_fst.comp (continuous_apply i)).smul (continuous_snd.comp (continuous_apply i))

namespace Submodule

instance topologicalAddGroup {R M : Type*} [Ring R] [AddCommGroup M] [Module R M]
    [TopologicalSpace M] [IsTopologicalAddGroup M] (S : Submodule R M) : IsTopologicalAddGroup S :=
  inferInstanceAs (IsTopologicalAddGroup S.toAddSubgroup)

end Submodule

section closure

variable {R : Type u} {M : Type v} [Semiring R] [TopologicalSpace M] [AddCommMonoid M] [Module R M]
  [ContinuousConstSMul R M]

theorem Submodule.mapsTo_smul_closure (s : Submodule R M) (c : R) :
    Set.MapsTo (c â€¢ Â·) (closure s : Set M) (closure s) :=
  have : Set.MapsTo (c â€¢ Â·) (s : Set M) s := fun _ h â†¦ s.smul_mem c h
  this.closure (continuous_const_smul c)

theorem Submodule.smul_closure_subset (s : Submodule R M) (c : R) :
    c â€¢ closure (s : Set M) âŠ† closure (s : Set M) :=
  (s.mapsTo_smul_closure c).image_subset

variable [ContinuousAdd M]

/-- The (topological-space) closure of a submodule of a topological `R`-module `M` is itself
a submodule. -/
def Submodule.topologicalClosure (s : Submodule R M) : Submodule R M :=
  { s.toAddSubmonoid.topologicalClosure with
    smul_mem' := s.mapsTo_smul_closure }

@[simp, norm_cast]
theorem Submodule.topologicalClosure_coe (s : Submodule R M) :
    (s.topologicalClosure : Set M) = closure (s : Set M) :=
  rfl

theorem Submodule.le_topologicalClosure (s : Submodule R M) : s â‰¤ s.topologicalClosure :=
  subset_closure

theorem Submodule.closure_subset_topologicalClosure_span (s : Set M) :
    closure s âŠ† (span R s).topologicalClosure := by
  rw [Submodule.topologicalClosure_coe]
  exact closure_mono subset_span

theorem Submodule.isClosed_topologicalClosure (s : Submodule R M) :
    IsClosed (s.topologicalClosure : Set M) := isClosed_closure

theorem Submodule.topologicalClosure_minimal (s : Submodule R M) {t : Submodule R M} (h : s â‰¤ t)
    (ht : IsClosed (t : Set M)) : s.topologicalClosure â‰¤ t :=
  closure_minimal h ht

theorem Submodule.topologicalClosure_mono {s : Submodule R M} {t : Submodule R M} (h : s â‰¤ t) :
    s.topologicalClosure â‰¤ t.topologicalClosure :=
  closure_mono h

/-- The topological closure of a closed submodule `s` is equal to `s`. -/
theorem IsClosed.submodule_topologicalClosure_eq {s : Submodule R M} (hs : IsClosed (s : Set M)) :
    s.topologicalClosure = s :=
  SetLike.ext' hs.closure_eq

/-- A subspace is dense iff its topological closure is the entire space. -/
theorem Submodule.dense_iff_topologicalClosure_eq_top {s : Submodule R M} :
    Dense (s : Set M) â†” s.topologicalClosure = âŠ¤ := by
  rw [â† SetLike.coe_set_eq, dense_iff_closure_eq]
  simp

instance Submodule.topologicalClosure.completeSpace {M' : Type*} [AddCommMonoid M'] [Module R M']
    [UniformSpace M'] [ContinuousAdd M'] [ContinuousConstSMul R M'] [CompleteSpace M']
    (U : Submodule R M') : CompleteSpace U.topologicalClosure :=
  isClosed_closure.completeSpace_coe

/-- A maximal proper subspace of a topological module (i.e a `Submodule` satisfying `IsCoatom`)
is either closed or dense. -/
theorem Submodule.isClosed_or_dense_of_isCoatom (s : Submodule R M) (hs : IsCoatom s) :
    IsClosed (s : Set M) âˆ¨ Dense (s : Set M) := by
  refine (hs.le_iff.mp s.le_topologicalClosure).symm.imp ?_ dense_iff_topologicalClosure_eq_top.mpr
  exact fun h â†¦ h â–¸ isClosed_closure

end closure

namespace Submodule

variable {Î¹ R : Type*} {M : Î¹ â†’ Type*} [Semiring R] [âˆ€ i, AddCommMonoid (M i)] [âˆ€ i, Module R (M i)]
  [âˆ€ i, TopologicalSpace (M i)] [DecidableEq Î¹]

/-- If `s i` is a family of submodules, each is in its module,
then the closure of their span in the indexed product of the modules
is the product of their closures.

In case of a finite index type, this statement immediately follows from `Submodule.iSup_map_single`.
However, the statement is true for an infinite index type as well. -/
theorem closure_coe_iSup_map_single (s : âˆ€ i, Submodule R (M i)) :
    closure (â†‘(â¨† i, (s i).map (LinearMap.single R M i)) : Set (âˆ€ i, M i)) =
      Set.univ.pi fun i â†¦ closure (s i) := by
  rw [â† closure_pi_set]
  refine (closure_mono ?_).antisymm <| closure_minimal ?_ isClosed_closure
  Â· exact SetLike.coe_mono <| iSup_map_single_le
  Â· simp only [Set.subset_def, mem_closure_iff]
    intro x hx U hU hxU
    rcases isOpen_pi_iff.mp hU x hxU with âŸ¨t, V, hV, hVUâŸ©
    refine âŸ¨âˆ‘ i âˆˆ t, Pi.single i (x i), hVU ?_, ?_âŸ©
    Â· simp_all [Finset.sum_pi_single]
    Â· exact sum_mem fun i hi â†¦ mem_iSup_of_mem i <| mem_map_of_mem <| hx _ <| Set.mem_univ _

/-- If `s i` is a family of submodules, each is in its module,
then the closure of their span in the indexed product of the modules
is the product of their closures.

In case of a finite index type, this statement immediately follows from `Submodule.iSup_map_single`.
However, the statement is true for an infinite index type as well.

This version is stated in terms of `Submodule.topologicalClosure`,
thus assumes that `M i`s are topological modules over `R`.
However, the statement is true without assuming continuity of the operations,
see `Submodule.closure_coe_iSup_map_single` above. -/
theorem topologicalClosure_iSup_map_single [âˆ€ i, ContinuousAdd (M i)]
    [âˆ€ i, ContinuousConstSMul R (M i)] (s : âˆ€ i, Submodule R (M i)) :
    topologicalClosure (â¨† i, (s i).map (LinearMap.single R M i)) =
      pi Set.univ fun i â†¦ (s i).topologicalClosure :=
  SetLike.coe_injective <| closure_coe_iSup_map_single _

end Submodule

section Pi

theorem LinearMap.continuous_on_pi {Î¹ : Type*} {R : Type*} {M : Type*} [Finite Î¹] [Semiring R]
    [TopologicalSpace R] [AddCommMonoid M] [Module R M] [TopologicalSpace M] [ContinuousAdd M]
    [ContinuousSMul R M] (f : (Î¹ â†’ R) â†’â‚—[R] M) : Continuous f := by
  cases nonempty_fintype Î¹
  classical
    -- for the proof, write `f` in the standard basis, and use that each coordinate is a continuous
    -- function.
    have : (f : (Î¹ â†’ R) â†’ M) = fun x => âˆ‘ i : Î¹, x i â€¢ f fun j => if i = j then 1 else 0 := by
      ext x
      exact f.pi_apply_eq_sum_univ x
    rw [this]
    refine continuous_finset_sum _ fun i _ => ?_
    exact (continuous_apply i).smul continuous_const

end Pi

section PointwiseLimits

variable {Mâ‚ Mâ‚‚ Î± R S : Type*} [TopologicalSpace Mâ‚‚] [T2Space Mâ‚‚] [Semiring R] [Semiring S]
  [AddCommMonoid Mâ‚] [AddCommMonoid Mâ‚‚] [Module R Mâ‚] [Module S Mâ‚‚] [ContinuousConstSMul S Mâ‚‚]

variable [ContinuousAdd Mâ‚‚] {Ïƒ : R â†’+* S} {l : Filter Î±}

/-- Constructs a bundled linear map from a function and a proof that this function belongs to the
closure of the set of linear maps. -/
@[simps -fullyApplied]
def linearMapOfMemClosureRangeCoe (f : Mâ‚ â†’ Mâ‚‚)
    (hf : f âˆˆ closure (Set.range ((â†‘) : (Mâ‚ â†’â‚›â‚—[Ïƒ] Mâ‚‚) â†’ Mâ‚ â†’ Mâ‚‚))) : Mâ‚ â†’â‚›â‚—[Ïƒ] Mâ‚‚ :=
  { addMonoidHomOfMemClosureRangeCoe f hf with
    map_smul' := (isClosed_setOf_map_smul Mâ‚ Mâ‚‚ Ïƒ).closure_subset_iff.2
      (Set.range_subset_iff.2 LinearMap.map_smulâ‚›â‚—) hf }

/-- Construct a bundled linear map from a pointwise limit of linear maps -/
@[simps! -fullyApplied]
def linearMapOfTendsto (f : Mâ‚ â†’ Mâ‚‚) (g : Î± â†’ Mâ‚ â†’â‚›â‚—[Ïƒ] Mâ‚‚) [l.NeBot]
    (h : Tendsto (fun a x => g a x) l (ğ“ f)) : Mâ‚ â†’â‚›â‚—[Ïƒ] Mâ‚‚ :=
  linearMapOfMemClosureRangeCoe f <|
    mem_closure_of_tendsto h <| Eventually.of_forall fun _ => Set.mem_range_self _

variable (Mâ‚ Mâ‚‚ Ïƒ)

theorem LinearMap.isClosed_range_coe : IsClosed (Set.range ((â†‘) : (Mâ‚ â†’â‚›â‚—[Ïƒ] Mâ‚‚) â†’ Mâ‚ â†’ Mâ‚‚)) :=
  isClosed_of_closure_subset fun f hf => âŸ¨linearMapOfMemClosureRangeCoe f hf, rflâŸ©

end PointwiseLimits

section Quotient

namespace Submodule

variable {R M : Type*} [Ring R] [AddCommGroup M] [Module R M] [TopologicalSpace M]
  (S : Submodule R M)

instance _root_.QuotientModule.Quotient.topologicalSpace : TopologicalSpace (M â§¸ S) :=
  inferInstanceAs (TopologicalSpace (Quotient S.quotientRel))

theorem isOpenMap_mkQ [ContinuousAdd M] : IsOpenMap S.mkQ :=
  QuotientAddGroup.isOpenMap_coe

theorem isOpenQuotientMap_mkQ [ContinuousAdd M] : IsOpenQuotientMap S.mkQ :=
  QuotientAddGroup.isOpenQuotientMap_mk

instance topologicalAddGroup_quotient [IsTopologicalAddGroup M] : IsTopologicalAddGroup (M â§¸ S) :=
  inferInstanceAs <| IsTopologicalAddGroup (M â§¸ S.toAddSubgroup)

instance continuousSMul_quotient [TopologicalSpace R] [IsTopologicalAddGroup M]
    [ContinuousSMul R M] : ContinuousSMul R (M â§¸ S) where
  continuous_smul := by
    rw [â† (IsOpenQuotientMap.id.prodMap S.isOpenQuotientMap_mkQ).continuous_comp_iff]
    exact continuous_quot_mk.comp continuous_smul

instance t3_quotient_of_isClosed [IsTopologicalAddGroup M] [IsClosed (S : Set M)] :
    T3Space (M â§¸ S) :=
  letI : IsClosed (S.toAddSubgroup : Set M) := â€¹_â€º
  QuotientAddGroup.instT3Space S.toAddSubgroup

end Submodule

end Quotient
