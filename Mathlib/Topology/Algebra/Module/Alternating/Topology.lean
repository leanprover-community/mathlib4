/-
Copyright (c) 2024 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Topology.Algebra.Module.Multilinear.Topology
import Mathlib.Topology.Algebra.Module.Alternating.Basic

/-!
# Topology on continuous alternating maps

In this file we define `UniformSpace` and `TopologicalSpace` structures
on the space of continuous alternating maps between topological vector spaces.

The structures are induced by those on `ContinuousMultilinearMap`s,
and most of the lemmas follow from the corresponding lemmas about `ContinuousMultilinearMap`s.
-/

open Bornology Function Set
open scoped Topology UniformConvergence Filter

namespace ContinuousAlternatingMap

variable {ğ•œ E F Î¹ : Type*} [NormedField ğ•œ]
  [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace E] [AddCommGroup F] [Module ğ•œ F]

section UniformAddGroup

variable [UniformSpace F] [UniformAddGroup F]

instance instUniformSpace : UniformSpace (E [â‹€^Î¹]â†’L[ğ•œ] F) :=
  .comap toContinuousMultilinearMap inferInstance

lemma uniformEmbedding_toContinuousMultilinearMap :
    UniformEmbedding (toContinuousMultilinearMap : (E [â‹€^Î¹]â†’L[ğ•œ] F) â†’ _) where
  inj := toContinuousMultilinearMap_injective
  comap_uniformity := rfl

lemma uniformContinuous_toContinuousMultilinearMap :
    UniformContinuous (toContinuousMultilinearMap : (E [â‹€^Î¹]â†’L[ğ•œ] F) â†’ _) :=
  uniformEmbedding_toContinuousMultilinearMap.uniformContinuous

theorem uniformContinuous_coe_fun [ContinuousSMul ğ•œ E] :
    UniformContinuous (DFunLike.coe : (E [â‹€^Î¹]â†’L[ğ•œ] F) â†’ (Î¹ â†’ E) â†’ F) :=
  ContinuousMultilinearMap.uniformContinuous_coe_fun.comp
    uniformContinuous_toContinuousMultilinearMap

theorem uniformContinuous_eval_const [ContinuousSMul ğ•œ E] (x : Î¹ â†’ E) :
    UniformContinuous fun f : E [â‹€^Î¹]â†’L[ğ•œ] F â†¦ f x :=
  uniformContinuous_pi.1 uniformContinuous_coe_fun x

instance instUniformAddGroup : UniformAddGroup (E [â‹€^Î¹]â†’L[ğ•œ] F) :=
  uniformEmbedding_toContinuousMultilinearMap.uniformAddGroup
    (toContinuousMultilinearMapLinear (R := â„•))

instance instUniformContinuousConstSMul {M : Type*}
    [Monoid M] [DistribMulAction M F] [SMulCommClass ğ•œ M F] [ContinuousConstSMul M F] :
    UniformContinuousConstSMul M (E [â‹€^Î¹]â†’L[ğ•œ] F) :=
  uniformEmbedding_toContinuousMultilinearMap.uniformContinuousConstSMul fun _ _ â†¦ rfl

section CompleteSpace

variable [ContinuousSMul ğ•œ E] [ContinuousConstSMul ğ•œ F] [CompleteSpace F] [T2Space F]

open UniformOnFun in
theorem completeSpace (h : RestrictGenTopology {s : Set (Î¹ â†’ E) | IsVonNBounded ğ•œ s}) :
    CompleteSpace (E [â‹€^Î¹]â†’L[ğ•œ] F) := by
  have := ContinuousMultilinearMap.completeSpace (F := F) h
  rw [completeSpace_iff_isComplete_range
    uniformEmbedding_toContinuousMultilinearMap.toUniformInducing, range_toContinuousMultilinearMap]
  simp only [setOf_forall]
  apply IsClosed.isComplete
  repeat refine isClosed_iInter fun _ â†¦ ?_
  exact isClosed_singleton.preimage (ContinuousMultilinearMap.continuous_eval_const _)

instance instCompleteSpace [TopologicalAddGroup E] [SequentialSpace (Î¹ â†’ E)] :
    CompleteSpace (E [â‹€^Î¹]â†’L[ğ•œ] F) :=
  completeSpace <| .of_seq fun _u x hux â†¦ (hux.isVonNBounded_range ğ•œ).insert x

end CompleteSpace

section RestrictScalars

variable (ğ•œ' : Type*) [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ' ğ•œ]
  [Module ğ•œ' E] [IsScalarTower ğ•œ' ğ•œ E] [Module ğ•œ' F] [IsScalarTower ğ•œ' ğ•œ F] [ContinuousSMul ğ•œ E]

theorem uniformEmbedding_restrictScalars :
    UniformEmbedding (restrictScalars ğ•œ' : E [â‹€^Î¹]â†’L[ğ•œ] F â†’ E [â‹€^Î¹]â†’L[ğ•œ'] F) := by
  rw [â† uniformEmbedding_toContinuousMultilinearMap.of_comp_iff]
  exact (ContinuousMultilinearMap.uniformEmbedding_restrictScalars ğ•œ').comp
    uniformEmbedding_toContinuousMultilinearMap

theorem uniformContinuous_restrictScalars :
    UniformContinuous (restrictScalars ğ•œ' : E [â‹€^Î¹]â†’L[ğ•œ] F â†’ E [â‹€^Î¹]â†’L[ğ•œ'] F) :=
  (uniformEmbedding_restrictScalars ğ•œ').uniformContinuous

end RestrictScalars

end UniformAddGroup

variable [TopologicalSpace F] [TopologicalAddGroup F]

instance instTopologicalSpace : TopologicalSpace (E [â‹€^Î¹]â†’L[ğ•œ] F) :=
  .induced toContinuousMultilinearMap inferInstance

lemma embedding_toContinuousMultilinearMap :
    Embedding (toContinuousMultilinearMap : (E [â‹€^Î¹]â†’L[ğ•œ] F â†’ _)) :=
  letI := TopologicalAddGroup.toUniformSpace F
  haveI := comm_topologicalAddGroup_is_uniform (G := F)
  uniformEmbedding_toContinuousMultilinearMap.embedding

@[continuity, fun_prop]
lemma continuous_toContinuousMultilinearMap :
    Continuous (toContinuousMultilinearMap : (E [â‹€^Î¹]â†’L[ğ•œ] F â†’ _)) :=
  embedding_toContinuousMultilinearMap.continuous

instance instContinuousConstSMul
    {M : Type*} [Monoid M] [DistribMulAction M F] [SMulCommClass ğ•œ M F] [ContinuousConstSMul M F] :
    ContinuousConstSMul M (E [â‹€^Î¹]â†’L[ğ•œ] F) :=
  embedding_toContinuousMultilinearMap.continuousConstSMul id rfl

instance instContinuousSMul [ContinuousSMul ğ•œ F] : ContinuousSMul ğ•œ (E [â‹€^Î¹]â†’L[ğ•œ] F) :=
  embedding_toContinuousMultilinearMap.continuousSMul continuous_id rfl

theorem hasBasis_nhds_zero_of_basis {Î¹' : Type*} {p : Î¹' â†’ Prop} {b : Î¹' â†’ Set F}
    (h : (ğ“ (0 : F)).HasBasis p b) :
    (ğ“ (0 : E [â‹€^Î¹]â†’L[ğ•œ] F)).HasBasis
      (fun Si : Set (Î¹ â†’ E) Ã— Î¹' => IsVonNBounded ğ•œ Si.1 âˆ§ p Si.2)
      fun Si => { f | MapsTo f Si.1 (b Si.2) } := by
  rw [nhds_induced]
  exact (ContinuousMultilinearMap.hasBasis_nhds_zero_of_basis h).comap _

theorem hasBasis_nhds_zero :
    (ğ“ (0 : E [â‹€^Î¹]â†’L[ğ•œ] F)).HasBasis
      (fun SV : Set (Î¹ â†’ E) Ã— Set F => IsVonNBounded ğ•œ SV.1 âˆ§ SV.2 âˆˆ ğ“ 0)
      fun SV => { f | MapsTo f SV.1 SV.2 } :=
  hasBasis_nhds_zero_of_basis (Filter.basis_sets _)

variable [ContinuousSMul ğ•œ E]

@[continuity, fun_prop]
theorem continuous_eval_const (x : Î¹ â†’ E) :
    Continuous fun p : E [â‹€^Î¹]â†’L[ğ•œ] F â†¦ p x :=
  (ContinuousMultilinearMap.continuous_eval_const x).comp continuous_toContinuousMultilinearMap

theorem continuous_coe_fun :
    Continuous (DFunLike.coe : E [â‹€^Î¹]â†’L[ğ•œ] F â†’ (Î¹ â†’ E) â†’ F) :=
  continuous_pi continuous_eval_const

instance instT2Space [T2Space F] : T2Space (E [â‹€^Î¹]â†’L[ğ•œ] F) :=
  .of_injective_continuous DFunLike.coe_injective continuous_coe_fun

instance instT3Space [T2Space F] : T2Space (E [â‹€^Î¹]â†’L[ğ•œ] F) :=
  letI : UniformSpace F := TopologicalAddGroup.toUniformSpace F
  haveI : UniformAddGroup F := comm_topologicalAddGroup_is_uniform
  inferInstance

section RestrictScalars

variable {ğ•œ' : Type*} [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ' ğ•œ]
  [Module ğ•œ' E] [IsScalarTower ğ•œ' ğ•œ E] [Module ğ•œ' F] [IsScalarTower ğ•œ' ğ•œ F]

theorem embedding_restrictScalars :
    Embedding (restrictScalars ğ•œ' : E [â‹€^Î¹]â†’L[ğ•œ] F â†’ E [â‹€^Î¹]â†’L[ğ•œ'] F) :=
  letI : UniformSpace F := TopologicalAddGroup.toUniformSpace F
  haveI : UniformAddGroup F := comm_topologicalAddGroup_is_uniform
  (uniformEmbedding_restrictScalars _).embedding

@[continuity, fun_prop]
theorem continuous_restrictScalars :
    Continuous
      (restrictScalars ğ•œ' : E [â‹€^Î¹]â†’L[ğ•œ] F â†’ E [â‹€^Î¹]â†’L[ğ•œ'] F) :=
   embedding_restrictScalars.continuous

variable (ğ•œ') in
/-- `ContinuousMultilinearMap.restrictScalars` as a `ContinuousLinearMap`. -/
@[simps (config := .asFn) apply]
def restrictScalarsCLM [ContinuousConstSMul ğ•œ' F] :
    E [â‹€^Î¹]â†’L[ğ•œ] F â†’L[ğ•œ'] E [â‹€^Î¹]â†’L[ğ•œ'] F where
  toFun := restrictScalars ğ•œ'
  map_add' _ _ := rfl
  map_smul' _ _ := rfl

end RestrictScalars

variable (ğ•œ E F)

/-- The application of a multilinear map as a `ContinuousLinearMap`. -/
def apply [ContinuousConstSMul ğ•œ F] (m : Î¹ â†’ E) : E [â‹€^Î¹]â†’L[ğ•œ] F â†’L[ğ•œ] F where
  toFun c := c m
  map_add' _ _ := rfl
  map_smul' _ _ := rfl
  cont := continuous_eval_const m

variable {ğ•œ E F}

@[simp]
lemma apply_apply [ContinuousConstSMul ğ•œ F] {m : Î¹ â†’ E} {c : E [â‹€^Î¹]â†’L[ğ•œ] F} :
    apply ğ•œ E F m c = c m := rfl

theorem hasSum_eval {Î± : Type*} {p : Î± â†’ E [â‹€^Î¹]â†’L[ğ•œ] F}
    {q : E [â‹€^Î¹]â†’L[ğ•œ] F} (h : HasSum p q) (m : Î¹ â†’ E) :
    HasSum (fun a => p a m) (q m) :=
  h.map (applyAddHom m) (continuous_eval_const m)

theorem tsum_eval [T2Space F] {Î± : Type*} {p : Î± â†’ E [â‹€^Î¹]â†’L[ğ•œ] F} (hp : Summable p)
    (m : Î¹ â†’ E) : (âˆ‘' a, p a) m = âˆ‘' a, p a m :=
  (hasSum_eval hp.hasSum m).tsum_eq.symm

end ContinuousAlternatingMap
