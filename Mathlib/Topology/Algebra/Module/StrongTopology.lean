/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker, Yury Kudryashov
-/
import Mathlib.Topology.Algebra.Module.Equiv
import Mathlib.Topology.Algebra.Module.UniformConvergence
import Mathlib.Topology.Algebra.SeparationQuotient.Section
import Mathlib.Topology.Hom.ContinuousEvalConst

/-!
# Strong topologies on the space of continuous linear maps

In this file, we define the strong topologies on `E â†’L[ğ•œ] F` associated with a family
`ğ”– : Set (Set E)` to be the topology of uniform convergence on the elements of `ğ”–` (also called
the topology of `ğ”–`-convergence).

The lemma `UniformOnFun.continuousSMul_of_image_bounded` tells us that this is a
vector space topology if the continuous linear image of any element of `ğ”–` is bounded (in the sense
of `Bornology.IsVonNBounded`).

We then declare an instance for the case where `ğ”–` is exactly the set of all bounded subsets of
`E`, giving us the so-called "topology of uniform convergence on bounded sets" (or "topology of
bounded convergence"), which coincides with the operator norm topology in the case of
`NormedSpace`s.

Other useful examples include the weak-* topology (when `ğ”–` is the set of finite sets or the set
of singletons) and the topology of compact convergence (when `ğ”–` is the set of relatively compact
sets).

## Main definitions

* `UniformConvergenceCLM` is a type synonym for `E â†’SL[Ïƒ] F` equipped with the `ğ”–`-topology.
* `UniformConvergenceCLM.instTopologicalSpace` is the topology mentioned above for an arbitrary `ğ”–`.
* `ContinuousLinearMap.topologicalSpace` is the topology of bounded convergence. This is
  declared as an instance.

## Main statements

* `UniformConvergenceCLM.instIsTopologicalAddGroup` and
  `UniformConvergenceCLM.instContinuousSMul` show that the strong topology
  makes `E â†’L[ğ•œ] F` a topological vector space, with the assumptions on `ğ”–` mentioned above.
* `ContinuousLinearMap.topologicalAddGroup` and
  `ContinuousLinearMap.continuousSMul` register these facts as instances for the special
  case of bounded convergence.

## References

* [N. Bourbaki, *Topological Vector Spaces*][bourbaki1987]

## TODO

* Add convergence on compact subsets

## Tags

uniform convergence, bounded convergence
-/

open Bornology Filter Function Set Topology
open scoped UniformConvergence Uniformity

section General

/-! ### ğ”–-Topologies -/

variable {ğ•œâ‚ ğ•œâ‚‚ : Type*} [NormedField ğ•œâ‚] [NormedField ğ•œâ‚‚] (Ïƒ : ğ•œâ‚ â†’+* ğ•œâ‚‚) {E F : Type*}
  [AddCommGroup E] [Module ğ•œâ‚ E] [TopologicalSpace E]
  [AddCommGroup F] [Module ğ•œâ‚‚ F]
variable (F)

/-- Given `E` and `F` two topological vector spaces and `ğ”– : Set (Set E)`, then
`UniformConvergenceCLM Ïƒ F ğ”–` is a type synonym of `E â†’SL[Ïƒ] F` equipped with the "topology of
uniform convergence on the elements of `ğ”–`".

If the continuous linear image of any element of `ğ”–` is bounded, this makes `E â†’SL[Ïƒ] F` a
topological vector space. -/
@[nolint unusedArguments]
def UniformConvergenceCLM [TopologicalSpace F] (_ : Set (Set E)) := E â†’SL[Ïƒ] F

namespace UniformConvergenceCLM

instance instFunLike [TopologicalSpace F] (ğ”– : Set (Set E)) :
    FunLike (UniformConvergenceCLM Ïƒ F ğ”–) E F :=
  ContinuousLinearMap.funLike

instance instContinuousSemilinearMapClass [TopologicalSpace F] (ğ”– : Set (Set E)) :
    ContinuousSemilinearMapClass (UniformConvergenceCLM Ïƒ F ğ”–) Ïƒ E F :=
  ContinuousLinearMap.continuousSemilinearMapClass

instance instTopologicalSpace [TopologicalSpace F] [IsTopologicalAddGroup F] (ğ”– : Set (Set E)) :
    TopologicalSpace (UniformConvergenceCLM Ïƒ F ğ”–) :=
  (@UniformOnFun.topologicalSpace E F (IsTopologicalAddGroup.toUniformSpace F) ğ”–).induced
    (DFunLike.coe : (UniformConvergenceCLM Ïƒ F ğ”–) â†’ (E â†’áµ¤[ğ”–] F))

theorem topologicalSpace_eq [UniformSpace F] [IsUniformAddGroup F] (ğ”– : Set (Set E)) :
    instTopologicalSpace Ïƒ F ğ”– = TopologicalSpace.induced (UniformOnFun.ofFun ğ”– âˆ˜ DFunLike.coe)
      (UniformOnFun.topologicalSpace E F ğ”–) := by
  rw [instTopologicalSpace]
  congr
  exact IsUniformAddGroup.toUniformSpace_eq

/-- The uniform structure associated with `ContinuousLinearMap.strongTopology`. We make sure
that this has nice definitional properties. -/
instance instUniformSpace [UniformSpace F] [IsUniformAddGroup F]
    (ğ”– : Set (Set E)) : UniformSpace (UniformConvergenceCLM Ïƒ F ğ”–) :=
  UniformSpace.replaceTopology
    ((UniformOnFun.uniformSpace E F ğ”–).comap (UniformOnFun.ofFun ğ”– âˆ˜ DFunLike.coe))
    (by rw [UniformConvergenceCLM.instTopologicalSpace, IsUniformAddGroup.toUniformSpace_eq]; rfl)

theorem uniformSpace_eq [UniformSpace F] [IsUniformAddGroup F] (ğ”– : Set (Set E)) :
    instUniformSpace Ïƒ F ğ”– =
      UniformSpace.comap (UniformOnFun.ofFun ğ”– âˆ˜ DFunLike.coe)
        (UniformOnFun.uniformSpace E F ğ”–) := by
  rw [instUniformSpace, UniformSpace.replaceTopology_eq]

@[simp]
theorem uniformity_toTopologicalSpace_eq [UniformSpace F] [IsUniformAddGroup F] (ğ”– : Set (Set E)) :
    (UniformConvergenceCLM.instUniformSpace Ïƒ F ğ”–).toTopologicalSpace =
      UniformConvergenceCLM.instTopologicalSpace Ïƒ F ğ”– :=
  rfl

theorem isUniformInducing_coeFn [UniformSpace F] [IsUniformAddGroup F] (ğ”– : Set (Set E)) :
    IsUniformInducing (Î± := UniformConvergenceCLM Ïƒ F ğ”–) (UniformOnFun.ofFun ğ”– âˆ˜ DFunLike.coe) :=
  âŸ¨rflâŸ©

theorem isUniformEmbedding_coeFn [UniformSpace F] [IsUniformAddGroup F] (ğ”– : Set (Set E)) :
    IsUniformEmbedding (Î± := UniformConvergenceCLM Ïƒ F ğ”–) (UniformOnFun.ofFun ğ”– âˆ˜ DFunLike.coe) :=
  âŸ¨isUniformInducing_coeFn .., DFunLike.coe_injectiveâŸ©

theorem isEmbedding_coeFn [UniformSpace F] [IsUniformAddGroup F] (ğ”– : Set (Set E)) :
    IsEmbedding (X := UniformConvergenceCLM Ïƒ F ğ”–) (Y := E â†’áµ¤[ğ”–] F)
      (UniformOnFun.ofFun ğ”– âˆ˜ DFunLike.coe) :=
  IsUniformEmbedding.isEmbedding (isUniformEmbedding_coeFn _ _ _)

instance instAddCommGroup [TopologicalSpace F] [IsTopologicalAddGroup F] (ğ”– : Set (Set E)) :
    AddCommGroup (UniformConvergenceCLM Ïƒ F ğ”–) := ContinuousLinearMap.addCommGroup

@[simp]
theorem coe_zero [TopologicalSpace F] [IsTopologicalAddGroup F] (ğ”– : Set (Set E)) :
    â‡‘(0 : UniformConvergenceCLM Ïƒ F ğ”–) = 0 :=
  rfl

instance instIsUniformAddGroup [UniformSpace F] [IsUniformAddGroup F] (ğ”– : Set (Set E)) :
    IsUniformAddGroup (UniformConvergenceCLM Ïƒ F ğ”–) := by
  let Ï† : (UniformConvergenceCLM Ïƒ F ğ”–) â†’+ E â†’áµ¤[ğ”–] F :=
    âŸ¨âŸ¨(DFunLike.coe : (UniformConvergenceCLM Ïƒ F ğ”–) â†’ E â†’áµ¤[ğ”–] F), rflâŸ©, fun _ _ => rflâŸ©
  exact (isUniformEmbedding_coeFn _ _ _).isUniformAddGroup Ï†

instance instIsTopologicalAddGroup [TopologicalSpace F] [IsTopologicalAddGroup F]
    (ğ”– : Set (Set E)) : IsTopologicalAddGroup (UniformConvergenceCLM Ïƒ F ğ”–) := by
  letI : UniformSpace F := IsTopologicalAddGroup.toUniformSpace F
  haveI : IsUniformAddGroup F := isUniformAddGroup_of_addCommGroup
  infer_instance

theorem continuousEvalConst [TopologicalSpace F] [IsTopologicalAddGroup F]
    (ğ”– : Set (Set E)) (hğ”– : â‹ƒâ‚€ ğ”– = Set.univ) :
    ContinuousEvalConst (UniformConvergenceCLM Ïƒ F ğ”–) E F where
  continuous_eval_const x := by
    letI : UniformSpace F := IsTopologicalAddGroup.toUniformSpace F
    haveI : IsUniformAddGroup F := isUniformAddGroup_of_addCommGroup
    exact (UniformOnFun.uniformContinuous_eval hğ”– x).continuous.comp
      (isEmbedding_coeFn Ïƒ F ğ”–).continuous

theorem t2Space [TopologicalSpace F] [IsTopologicalAddGroup F] [T2Space F]
    (ğ”– : Set (Set E)) (hğ”– : â‹ƒâ‚€ ğ”– = univ) : T2Space (UniformConvergenceCLM Ïƒ F ğ”–) := by
  letI : UniformSpace F := IsTopologicalAddGroup.toUniformSpace F
  haveI : IsUniformAddGroup F := isUniformAddGroup_of_addCommGroup
  haveI : T2Space (E â†’áµ¤[ğ”–] F) := UniformOnFun.t2Space_of_covering hğ”–
  exact (isEmbedding_coeFn Ïƒ F ğ”–).t2Space

instance instDistribMulAction (M : Type*) [Monoid M] [DistribMulAction M F] [SMulCommClass ğ•œâ‚‚ M F]
    [TopologicalSpace F] [IsTopologicalAddGroup F] [ContinuousConstSMul M F] (ğ”– : Set (Set E)) :
    DistribMulAction M (UniformConvergenceCLM Ïƒ F ğ”–) := ContinuousLinearMap.distribMulAction

instance instModule (R : Type*) [Semiring R] [Module R F] [SMulCommClass ğ•œâ‚‚ R F]
    [TopologicalSpace F] [ContinuousConstSMul R F] [IsTopologicalAddGroup F] (ğ”– : Set (Set E)) :
    Module R (UniformConvergenceCLM Ïƒ F ğ”–) := ContinuousLinearMap.module

theorem continuousSMul [RingHomSurjective Ïƒ] [RingHomIsometric Ïƒ]
    [TopologicalSpace F] [IsTopologicalAddGroup F] [ContinuousSMul ğ•œâ‚‚ F] (ğ”– : Set (Set E))
    (hğ”–â‚ƒ : âˆ€ S âˆˆ ğ”–, IsVonNBounded ğ•œâ‚ S) :
    ContinuousSMul ğ•œâ‚‚ (UniformConvergenceCLM Ïƒ F ğ”–) := by
  letI : UniformSpace F := IsTopologicalAddGroup.toUniformSpace F
  haveI : IsUniformAddGroup F := isUniformAddGroup_of_addCommGroup
  let Ï† : (UniformConvergenceCLM Ïƒ F ğ”–) â†’â‚—[ğ•œâ‚‚] E â†’ F :=
    âŸ¨âŸ¨DFunLike.coe, fun _ _ => rflâŸ©, fun _ _ => rflâŸ©
  exact UniformOnFun.continuousSMul_induced_of_image_bounded ğ•œâ‚‚ E F (UniformConvergenceCLM Ïƒ F ğ”–) Ï†
    âŸ¨rflâŸ© fun u s hs => (hğ”–â‚ƒ s hs).image u

theorem hasBasis_nhds_zero_of_basis [TopologicalSpace F] [IsTopologicalAddGroup F]
    {Î¹ : Type*} (ğ”– : Set (Set E)) (hğ”–â‚ : ğ”–.Nonempty) (hğ”–â‚‚ : DirectedOn (Â· âŠ† Â·) ğ”–) {p : Î¹ â†’ Prop}
    {b : Î¹ â†’ Set F} (h : (ğ“ 0 : Filter F).HasBasis p b) :
    (ğ“ (0 : UniformConvergenceCLM Ïƒ F ğ”–)).HasBasis
      (fun Si : Set E Ã— Î¹ => Si.1 âˆˆ ğ”– âˆ§ p Si.2)
      fun Si => { f : E â†’SL[Ïƒ] F | âˆ€ x âˆˆ Si.1, f x âˆˆ b Si.2 } := by
  letI : UniformSpace F := IsTopologicalAddGroup.toUniformSpace F
  haveI : IsUniformAddGroup F := isUniformAddGroup_of_addCommGroup
  rw [(isEmbedding_coeFn Ïƒ F ğ”–).isInducing.nhds_eq_comap]
  exact (UniformOnFun.hasBasis_nhds_zero_of_basis ğ”– hğ”–â‚ hğ”–â‚‚ h).comap DFunLike.coe

theorem hasBasis_nhds_zero [TopologicalSpace F] [IsTopologicalAddGroup F]
    (ğ”– : Set (Set E)) (hğ”–â‚ : ğ”–.Nonempty) (hğ”–â‚‚ : DirectedOn (Â· âŠ† Â·) ğ”–) :
    (ğ“ (0 : UniformConvergenceCLM Ïƒ F ğ”–)).HasBasis
      (fun SV : Set E Ã— Set F => SV.1 âˆˆ ğ”– âˆ§ SV.2 âˆˆ (ğ“ 0 : Filter F)) fun SV =>
      { f : UniformConvergenceCLM Ïƒ F ğ”– | âˆ€ x âˆˆ SV.1, f x âˆˆ SV.2 } :=
  hasBasis_nhds_zero_of_basis Ïƒ F ğ”– hğ”–â‚ hğ”–â‚‚ (ğ“ 0).basis_sets

theorem nhds_zero_eq_of_basis [TopologicalSpace F] [IsTopologicalAddGroup F] (ğ”– : Set (Set E))
    {Î¹ : Type*} {p : Î¹ â†’ Prop} {b : Î¹ â†’ Set F} (h : (ğ“ 0 : Filter F).HasBasis p b) :
    ğ“ (0 : UniformConvergenceCLM Ïƒ F ğ”–) =
      â¨… (s : Set E) (_ : s âˆˆ ğ”–) (i : Î¹) (_ : p i),
        ğ“Ÿ {f : UniformConvergenceCLM Ïƒ F ğ”– | MapsTo f s (b i)} := by
  letI : UniformSpace F := IsTopologicalAddGroup.toUniformSpace F
  haveI : IsUniformAddGroup F := isUniformAddGroup_of_addCommGroup
  rw [(isEmbedding_coeFn Ïƒ F ğ”–).isInducing.nhds_eq_comap,
    UniformOnFun.nhds_eq_of_basis _ _ h.uniformity_of_nhds_zero]
  simp [MapsTo]

theorem nhds_zero_eq [TopologicalSpace F] [IsTopologicalAddGroup F] (ğ”– : Set (Set E)) :
    ğ“ (0 : UniformConvergenceCLM Ïƒ F ğ”–) =
      â¨… s âˆˆ ğ”–, â¨… t âˆˆ ğ“ (0 : F),
        ğ“Ÿ {f : UniformConvergenceCLM Ïƒ F ğ”– | MapsTo f s t} :=
  nhds_zero_eq_of_basis _ _ _ (ğ“ 0).basis_sets

variable {F} in
theorem eventually_nhds_zero_mapsTo [TopologicalSpace F] [IsTopologicalAddGroup F]
    {ğ”– : Set (Set E)} {s : Set E} (hs : s âˆˆ ğ”–) {U : Set F} (hu : U âˆˆ ğ“ 0) :
    âˆ€á¶  f : UniformConvergenceCLM Ïƒ F ğ”– in ğ“ 0, MapsTo f s U := by
  rw [nhds_zero_eq]
  apply_rules [mem_iInf_of_mem, mem_principal_self]

variable {Ïƒ F} in
theorem isVonNBounded_image2_apply {R : Type*} [SeminormedRing R]
    [TopologicalSpace F] [IsTopologicalAddGroup F]
    [DistribMulAction R F] [ContinuousConstSMul R F] [SMulCommClass ğ•œâ‚‚ R F]
    {ğ”– : Set (Set E)} {S : Set (UniformConvergenceCLM Ïƒ F ğ”–)} (hS : IsVonNBounded R S)
    {s : Set E} (hs : s âˆˆ ğ”–) : IsVonNBounded R (Set.image2 (fun f x â†¦ f x) S s) := by
  intro U hU
  filter_upwards [hS (eventually_nhds_zero_mapsTo Ïƒ hs hU)] with c hc
  rw [image2_subset_iff]
  intro f hf x hx
  rcases hc hf with âŸ¨g, hg, rflâŸ©
  exact smul_mem_smul_set (hg hx)

variable {Ïƒ F} in
/-- A set `S` of continuous linear maps with topology of uniform convergence on sets `s âˆˆ ğ”–`
is von Neumann bounded iff for any `s âˆˆ ğ”–`,
the set `{f x | (f âˆˆ S) (x âˆˆ s)}` is von Neumann bounded. -/
theorem isVonNBounded_iff {R : Type*} [NormedDivisionRing R]
    [TopologicalSpace F] [IsTopologicalAddGroup F]
    [Module R F] [ContinuousConstSMul R F] [SMulCommClass ğ•œâ‚‚ R F]
    {ğ”– : Set (Set E)} {S : Set (UniformConvergenceCLM Ïƒ F ğ”–)} :
    IsVonNBounded R S â†” âˆ€ s âˆˆ ğ”–, IsVonNBounded R (Set.image2 (fun f x â†¦ f x) S s) := by
  refine âŸ¨fun hS s hs â†¦ isVonNBounded_image2_apply hS hs, fun h â†¦ ?_âŸ©
  simp_rw [isVonNBounded_iff_absorbing_le, nhds_zero_eq, le_iInf_iff, le_principal_iff]
  intro s hs U hU
  rw [Filter.mem_absorbing, Absorbs]
  filter_upwards [h s hs hU, eventually_ne_cobounded 0] with c hc hcâ‚€ f hf
  rw [mem_smul_set_iff_inv_smul_memâ‚€ hcâ‚€]
  intro x hx
  simpa only [mem_smul_set_iff_inv_smul_memâ‚€ hcâ‚€] using hc (mem_image2_of_mem hf hx)

instance instUniformContinuousConstSMul (M : Type*)
    [Monoid M] [DistribMulAction M F] [SMulCommClass ğ•œâ‚‚ M F]
    [UniformSpace F] [IsUniformAddGroup F] [UniformContinuousConstSMul M F] (ğ”– : Set (Set E)) :
    UniformContinuousConstSMul M (UniformConvergenceCLM Ïƒ F ğ”–) :=
  (isUniformInducing_coeFn Ïƒ F ğ”–).uniformContinuousConstSMul fun _ _ â†¦ by rfl

instance instContinuousConstSMul (M : Type*)
    [Monoid M] [DistribMulAction M F] [SMulCommClass ğ•œâ‚‚ M F]
    [TopologicalSpace F] [IsTopologicalAddGroup F] [ContinuousConstSMul M F] (ğ”– : Set (Set E)) :
    ContinuousConstSMul M (UniformConvergenceCLM Ïƒ F ğ”–) :=
  let _ := IsTopologicalAddGroup.toUniformSpace F
  have _ : IsUniformAddGroup F := isUniformAddGroup_of_addCommGroup
  have _ := uniformContinuousConstSMul_of_continuousConstSMul M F
  inferInstance

theorem tendsto_iff_tendstoUniformlyOn {Î¹ : Type*} {p : Filter Î¹} [UniformSpace F]
    [IsUniformAddGroup F] (ğ”– : Set (Set E)) {a : Î¹ â†’ UniformConvergenceCLM Ïƒ F ğ”–}
    {aâ‚€ : UniformConvergenceCLM Ïƒ F ğ”–} :
    Filter.Tendsto a p (ğ“ aâ‚€) â†” âˆ€ s âˆˆ ğ”–, TendstoUniformlyOn (a Â· Â·) aâ‚€ p s := by
  rw [(isEmbedding_coeFn Ïƒ F ğ”–).tendsto_nhds_iff, UniformOnFun.tendsto_iff_tendstoUniformlyOn]
  rfl

variable {F} in
theorem isUniformInducing_postcomp
    {G : Type*} [AddCommGroup G] [UniformSpace G] [IsUniformAddGroup G]
    {ğ•œâ‚ƒ : Type*} [NormedField ğ•œâ‚ƒ] [Module ğ•œâ‚ƒ G]
    {Ï„ : ğ•œâ‚‚ â†’+* ğ•œâ‚ƒ} {Ï : ğ•œâ‚ â†’+* ğ•œâ‚ƒ} [RingHomCompTriple Ïƒ Ï„ Ï] [UniformSpace F] [IsUniformAddGroup F]
    (g : F â†’SL[Ï„] G) (hg : IsUniformInducing g) (ğ”– : Set (Set E)) :
    IsUniformInducing (Î± := UniformConvergenceCLM Ïƒ F ğ”–) (Î² := UniformConvergenceCLM Ï G ğ”–)
      g.comp := by
  rw [â† (isUniformInducing_coeFn _ _ _).of_comp_iff]
  exact (UniformOnFun.postcomp_isUniformInducing hg).comp (isUniformInducing_coeFn _ _ _)

theorem completeSpace [UniformSpace F] [IsUniformAddGroup F] [ContinuousSMul ğ•œâ‚‚ F] [CompleteSpace F]
    {ğ”– : Set (Set E)} (hğ”– : IsCoherentWith ğ”–) (hğ”–U : â‹ƒâ‚€ ğ”– = univ) :
    CompleteSpace (UniformConvergenceCLM Ïƒ F ğ”–) := by
  wlog hF : T2Space F generalizing F
  Â· rw [(isUniformInducing_postcomp Ïƒ (SeparationQuotient.mkCLM ğ•œâ‚‚ F)
      SeparationQuotient.isUniformInducing_mk _).completeSpace_congr]
    exacts [this _ inferInstance, SeparationQuotient.postcomp_mkCLM_surjective F Ïƒ E]
  rw [completeSpace_iff_isComplete_range (isUniformInducing_coeFn _ _ _)]
  apply IsClosed.isComplete
  have Hâ‚ : IsClosed {f : E â†’áµ¤[ğ”–] F | Continuous ((UniformOnFun.toFun ğ”–) f)} :=
    UniformOnFun.isClosed_setOf_continuous hğ”–
  convert Hâ‚.inter <| (LinearMap.isClosed_range_coe E F Ïƒ).preimage
    (UniformOnFun.uniformContinuous_toFun hğ”–U).continuous
  exact ContinuousLinearMap.range_coeFn_eq

variable {ğ”–â‚ ğ”–â‚‚ : Set (Set E)}

theorem uniformSpace_mono [UniformSpace F] [IsUniformAddGroup F] (h : ğ”–â‚‚ âŠ† ğ”–â‚) :
    instUniformSpace Ïƒ F ğ”–â‚ â‰¤ instUniformSpace Ïƒ F ğ”–â‚‚ := by
  simp_rw [uniformSpace_eq]
  exact UniformSpace.comap_mono (UniformOnFun.mono (le_refl _) h)

theorem topologicalSpace_mono [TopologicalSpace F] [IsTopologicalAddGroup F] (h : ğ”–â‚‚ âŠ† ğ”–â‚) :
    instTopologicalSpace Ïƒ F ğ”–â‚ â‰¤ instTopologicalSpace Ïƒ F ğ”–â‚‚ := by
  letI := IsTopologicalAddGroup.toUniformSpace F
  haveI : IsUniformAddGroup F := isUniformAddGroup_of_addCommGroup
  simp_rw [â† uniformity_toTopologicalSpace_eq]
  exact UniformSpace.toTopologicalSpace_mono (uniformSpace_mono Ïƒ F h)

end UniformConvergenceCLM

end General

namespace ContinuousLinearMap

section BoundedSets

/-! ### Topology of bounded convergence  -/

variable {ğ•œâ‚ ğ•œâ‚‚ ğ•œâ‚ƒ : Type*} [NormedField ğ•œâ‚] [NormedField ğ•œâ‚‚] [NormedField ğ•œâ‚ƒ] {Ïƒ : ğ•œâ‚ â†’+* ğ•œâ‚‚}
  {Ï„ : ğ•œâ‚‚ â†’+* ğ•œâ‚ƒ} {Ï : ğ•œâ‚ â†’+* ğ•œâ‚ƒ} [RingHomCompTriple Ïƒ Ï„ Ï] {E F G : Type*} [AddCommGroup E]
  [Module ğ•œâ‚ E] [AddCommGroup F] [Module ğ•œâ‚‚ F]
  [AddCommGroup G] [Module ğ•œâ‚ƒ G] [TopologicalSpace E]

/-- The topology of bounded convergence on `E â†’L[ğ•œ] F`. This coincides with the topology induced by
the operator norm when `E` and `F` are normed spaces. -/
instance topologicalSpace [TopologicalSpace F] [IsTopologicalAddGroup F] :
    TopologicalSpace (E â†’SL[Ïƒ] F) :=
  UniformConvergenceCLM.instTopologicalSpace Ïƒ F { S | IsVonNBounded ğ•œâ‚ S }

instance topologicalAddGroup [TopologicalSpace F] [IsTopologicalAddGroup F] :
    IsTopologicalAddGroup (E â†’SL[Ïƒ] F) :=
  UniformConvergenceCLM.instIsTopologicalAddGroup Ïƒ F _

instance continuousSMul [RingHomSurjective Ïƒ] [RingHomIsometric Ïƒ] [TopologicalSpace F]
    [IsTopologicalAddGroup F] [ContinuousSMul ğ•œâ‚‚ F] : ContinuousSMul ğ•œâ‚‚ (E â†’SL[Ïƒ] F) :=
  UniformConvergenceCLM.continuousSMul Ïƒ F { S | IsVonNBounded ğ•œâ‚ S } fun _ hs => hs

instance uniformSpace [UniformSpace F] [IsUniformAddGroup F] : UniformSpace (E â†’SL[Ïƒ] F) :=
  UniformConvergenceCLM.instUniformSpace Ïƒ F { S | IsVonNBounded ğ•œâ‚ S }

instance isUniformAddGroup [UniformSpace F] [IsUniformAddGroup F] :
    IsUniformAddGroup (E â†’SL[Ïƒ] F) :=
  UniformConvergenceCLM.instIsUniformAddGroup Ïƒ F _

instance instContinuousEvalConst [TopologicalSpace F] [IsTopologicalAddGroup F]
    [ContinuousSMul ğ•œâ‚ E] : ContinuousEvalConst (E â†’SL[Ïƒ] F) E F :=
  UniformConvergenceCLM.continuousEvalConst Ïƒ F _ Bornology.isVonNBounded_covers

instance instT2Space [TopologicalSpace F] [IsTopologicalAddGroup F] [ContinuousSMul ğ•œâ‚ E]
    [T2Space F] : T2Space (E â†’SL[Ïƒ] F) :=
  UniformConvergenceCLM.t2Space Ïƒ F _ Bornology.isVonNBounded_covers

protected theorem hasBasis_nhds_zero_of_basis [TopologicalSpace F] [IsTopologicalAddGroup F]
    {Î¹ : Type*} {p : Î¹ â†’ Prop} {b : Î¹ â†’ Set F} (h : (ğ“ 0 : Filter F).HasBasis p b) :
    (ğ“ (0 : E â†’SL[Ïƒ] F)).HasBasis (fun Si : Set E Ã— Î¹ => IsVonNBounded ğ•œâ‚ Si.1 âˆ§ p Si.2)
      fun Si => { f : E â†’SL[Ïƒ] F | âˆ€ x âˆˆ Si.1, f x âˆˆ b Si.2 } :=
  UniformConvergenceCLM.hasBasis_nhds_zero_of_basis Ïƒ F { S | IsVonNBounded ğ•œâ‚ S }
    âŸ¨âˆ…, isVonNBounded_empty ğ•œâ‚ EâŸ©
    (directedOn_of_sup_mem fun _ _ => IsVonNBounded.union) h

protected theorem hasBasis_nhds_zero [TopologicalSpace F] [IsTopologicalAddGroup F] :
    (ğ“ (0 : E â†’SL[Ïƒ] F)).HasBasis
      (fun SV : Set E Ã— Set F => IsVonNBounded ğ•œâ‚ SV.1 âˆ§ SV.2 âˆˆ (ğ“ 0 : Filter F))
      fun SV => { f : E â†’SL[Ïƒ] F | âˆ€ x âˆˆ SV.1, f x âˆˆ SV.2 } :=
  ContinuousLinearMap.hasBasis_nhds_zero_of_basis (ğ“ 0).basis_sets

theorem isUniformEmbedding_toUniformOnFun [UniformSpace F] [IsUniformAddGroup F] :
    IsUniformEmbedding
      fun f : E â†’SL[Ïƒ] F â†¦ UniformOnFun.ofFun {s | Bornology.IsVonNBounded ğ•œâ‚ s} f :=
  UniformConvergenceCLM.isUniformEmbedding_coeFn ..

instance uniformContinuousConstSMul
    {M : Type*} [Monoid M] [DistribMulAction M F] [SMulCommClass ğ•œâ‚‚ M F]
    [UniformSpace F] [IsUniformAddGroup F] [UniformContinuousConstSMul M F] :
    UniformContinuousConstSMul M (E â†’SL[Ïƒ] F) :=
  UniformConvergenceCLM.instUniformContinuousConstSMul Ïƒ F _ _

instance continuousConstSMul {M : Type*} [Monoid M] [DistribMulAction M F] [SMulCommClass ğ•œâ‚‚ M F]
    [TopologicalSpace F] [IsTopologicalAddGroup F] [ContinuousConstSMul M F] :
    ContinuousConstSMul M (E â†’SL[Ïƒ] F) :=
  UniformConvergenceCLM.instContinuousConstSMul Ïƒ F _ _

protected theorem nhds_zero_eq_of_basis [TopologicalSpace F] [IsTopologicalAddGroup F]
    {Î¹ : Type*} {p : Î¹ â†’ Prop} {b : Î¹ â†’ Set F} (h : (ğ“ 0 : Filter F).HasBasis p b) :
    ğ“ (0 : E â†’SL[Ïƒ] F) =
      â¨… (s : Set E) (_ : IsVonNBounded ğ•œâ‚ s) (i : Î¹) (_ : p i),
        ğ“Ÿ {f : E â†’SL[Ïƒ] F | MapsTo f s (b i)} :=
  UniformConvergenceCLM.nhds_zero_eq_of_basis _ _ _ h

protected theorem nhds_zero_eq [TopologicalSpace F] [IsTopologicalAddGroup F] :
    ğ“ (0 : E â†’SL[Ïƒ] F) =
      â¨… (s : Set E) (_ : IsVonNBounded ğ•œâ‚ s) (U : Set F) (_ : U âˆˆ ğ“ 0),
        ğ“Ÿ {f : E â†’SL[Ïƒ] F | MapsTo f s U} :=
  UniformConvergenceCLM.nhds_zero_eq ..

/-- If `s` is a von Neumann bounded set and `U` is a neighbourhood of zero,
then sufficiently small continuous linear maps map `s` to `U`. -/
theorem eventually_nhds_zero_mapsTo [TopologicalSpace F] [IsTopologicalAddGroup F]
    {s : Set E} (hs : IsVonNBounded ğ•œâ‚ s) {U : Set F} (hu : U âˆˆ ğ“ 0) :
    âˆ€á¶  f : E â†’SL[Ïƒ] F in ğ“ 0, MapsTo f s U :=
  UniformConvergenceCLM.eventually_nhds_zero_mapsTo _ hs hu

/-- If `S` is a von Neumann bounded set of continuous linear maps `f : E â†’SL[Ïƒ] F`
and `s` is a von Neumann bounded set in the domain,
then the set `{f x | (f âˆˆ S) (x âˆˆ s)}` is von Neumann bounded.

See also `isVonNBounded_iff` for an `Iff` version with stronger typeclass assumptions. -/
theorem isVonNBounded_image2_apply {R : Type*} [SeminormedRing R]
    [TopologicalSpace F] [IsTopologicalAddGroup F]
    [DistribMulAction R F] [ContinuousConstSMul R F] [SMulCommClass ğ•œâ‚‚ R F]
    {S : Set (E â†’SL[Ïƒ] F)} (hS : IsVonNBounded R S) {s : Set E} (hs : IsVonNBounded ğ•œâ‚ s) :
    IsVonNBounded R (Set.image2 (fun f x â†¦ f x) S s) :=
  UniformConvergenceCLM.isVonNBounded_image2_apply hS hs

/-- A set `S` of continuous linear maps is von Neumann bounded
iff for any von Neumann bounded set `s`,
the set `{f x | (f âˆˆ S) (x âˆˆ s)}` is von Neumann bounded.

For the forward implication with weaker typeclass assumptions, see `isVonNBounded_image2_apply`. -/
theorem isVonNBounded_iff {R : Type*} [NormedDivisionRing R]
    [TopologicalSpace F] [IsTopologicalAddGroup F]
    [Module R F] [ContinuousConstSMul R F] [SMulCommClass ğ•œâ‚‚ R F]
    {S : Set (E â†’SL[Ïƒ] F)} :
    IsVonNBounded R S â†”
      âˆ€ s, IsVonNBounded ğ•œâ‚ s â†’ IsVonNBounded R (Set.image2 (fun f x â†¦ f x) S s) :=
  UniformConvergenceCLM.isVonNBounded_iff

theorem completeSpace [UniformSpace F] [IsUniformAddGroup F] [ContinuousSMul ğ•œâ‚‚ F] [CompleteSpace F]
    [ContinuousSMul ğ•œâ‚ E] (h : IsCoherentWith {s : Set E | IsVonNBounded ğ•œâ‚ s}) :
    CompleteSpace (E â†’SL[Ïƒ] F) :=
  UniformConvergenceCLM.completeSpace _ _ h isVonNBounded_covers

instance instCompleteSpace [IsTopologicalAddGroup E] [ContinuousSMul ğ•œâ‚ E] [SequentialSpace E]
    [UniformSpace F] [IsUniformAddGroup F] [ContinuousSMul ğ•œâ‚‚ F] [CompleteSpace F] :
    CompleteSpace (E â†’SL[Ïƒ] F) :=
  completeSpace <| .of_seq fun _ _ h â†¦ (h.isVonNBounded_range ğ•œâ‚).insert _

variable (G) [TopologicalSpace F] [TopologicalSpace G]

/-- Pre-composition by a *fixed* continuous linear map as a continuous linear map.
Note that in non-normed space it is not always true that composition is continuous
in both variables, so we have to fix one of them. -/
@[simps]
def precomp [IsTopologicalAddGroup G] [ContinuousConstSMul ğ•œâ‚ƒ G] [RingHomSurjective Ïƒ]
    [RingHomIsometric Ïƒ] (L : E â†’SL[Ïƒ] F) : (F â†’SL[Ï„] G) â†’L[ğ•œâ‚ƒ] E â†’SL[Ï] G where
  toFun f := f.comp L
  map_add' f g := add_comp f g L
  map_smul' a f := smul_comp a f L
  cont := by
    letI : UniformSpace G := IsTopologicalAddGroup.toUniformSpace G
    haveI : IsUniformAddGroup G := isUniformAddGroup_of_addCommGroup
    rw [(UniformConvergenceCLM.isEmbedding_coeFn _ _ _).continuous_iff]
    apply (UniformOnFun.precomp_uniformContinuous fun S hS => hS.image L).continuous.comp
        (UniformConvergenceCLM.isEmbedding_coeFn _ _ _).continuous

variable (E) {G}

/-- Post-composition by a *fixed* continuous linear map as a continuous linear map.
Note that in non-normed space it is not always true that composition is continuous
in both variables, so we have to fix one of them. -/
@[simps]
def postcomp [IsTopologicalAddGroup F] [IsTopologicalAddGroup G] [ContinuousConstSMul ğ•œâ‚ƒ G]
    [ContinuousConstSMul ğ•œâ‚‚ F] (L : F â†’SL[Ï„] G) : (E â†’SL[Ïƒ] F) â†’SL[Ï„] E â†’SL[Ï] G where
  toFun f := L.comp f
  map_add' := comp_add L
  map_smul' := comp_smulâ‚›â‚— L
  cont := by
    letI : UniformSpace G := IsTopologicalAddGroup.toUniformSpace G
    haveI : IsUniformAddGroup G := isUniformAddGroup_of_addCommGroup
    letI : UniformSpace F := IsTopologicalAddGroup.toUniformSpace F
    haveI : IsUniformAddGroup F := isUniformAddGroup_of_addCommGroup
    rw [(UniformConvergenceCLM.isEmbedding_coeFn _ _ _).continuous_iff]
    exact
      (UniformOnFun.postcomp_uniformContinuous L.uniformContinuous).continuous.comp
        (UniformConvergenceCLM.isEmbedding_coeFn _ _ _).continuous

end BoundedSets

section BilinearMaps

variable {ğ•œ ğ•œâ‚‚ ğ•œâ‚ƒ : Type*} [NormedField ğ•œ] [NormedField ğ•œâ‚‚] [NormedField ğ•œâ‚ƒ] {E F G : Type*}
  [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace E]
  [AddCommGroup F] [Module ğ•œâ‚‚ F] [TopologicalSpace F]
  [AddCommGroup G] [Module ğ•œâ‚ƒ G]
  [TopologicalSpace G] [IsTopologicalAddGroup G] [ContinuousConstSMul ğ•œâ‚ƒ G]
  {Ïƒâ‚â‚ƒ : ğ•œ â†’+* ğ•œâ‚ƒ} {Ïƒâ‚‚â‚ƒ : ğ•œâ‚‚ â†’+* ğ•œâ‚ƒ}

/-- Send a continuous sesquilinear map to an abstract sesquilinear map (forgetting continuity). -/
def toLinearMapâ‚â‚‚ (L : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) : E â†’â‚›â‚—[Ïƒâ‚â‚ƒ] F â†’â‚›â‚—[Ïƒâ‚‚â‚ƒ] G :=
  (coeLMâ‚›â‚— Ïƒâ‚‚â‚ƒ).comp L.toLinearMap

@[deprecated (since := "2025-07-28")] alias toLinearMapâ‚‚ := toLinearMapâ‚â‚‚

@[simp] lemma toLinearMapâ‚â‚‚_apply (L : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) (v : E) (w : F) :
    L.toLinearMapâ‚â‚‚ v w = L v w := rfl

@[deprecated (since := "2025-07-28")] alias toLinearMapâ‚‚_apply := toLinearMapâ‚â‚‚_apply

/-- Send a continuous bilinear form to an abstract bilinear form (forgetting continuity). -/
def toBilinForm (L : E â†’L[ğ•œ] E â†’L[ğ•œ] ğ•œ) : LinearMap.BilinForm ğ•œ E := L.toLinearMapâ‚â‚‚

@[simp] lemma toBilinForm_apply (L : E â†’L[ğ•œ] E â†’L[ğ•œ] ğ•œ) (v : E) (w : E) :
    L.toBilinForm v w = L v w := rfl

end BilinearMaps

section RestrictScalars

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [AddCommGroup E] [TopologicalSpace E] [Module ğ•œ E] [ContinuousSMul ğ•œ E]
  {F : Type*} [AddCommGroup F]

section UniformSpace

variable [UniformSpace F] [IsUniformAddGroup F] [Module ğ•œ F]
  (ğ•œ' : Type*) [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ' ğ•œ]
  [Module ğ•œ' E] [IsScalarTower ğ•œ' ğ•œ E] [Module ğ•œ' F] [IsScalarTower ğ•œ' ğ•œ F]

theorem isUniformEmbedding_restrictScalars :
    IsUniformEmbedding (restrictScalars ğ•œ' : (E â†’L[ğ•œ] F) â†’ (E â†’L[ğ•œ'] F)) := by
  rw [â† isUniformEmbedding_toUniformOnFun.of_comp_iff]
  convert isUniformEmbedding_toUniformOnFun using 4 with s
  exact âŸ¨fun h â†¦ h.extend_scalars _, fun h â†¦ h.restrict_scalars _âŸ©

theorem uniformContinuous_restrictScalars :
    UniformContinuous (restrictScalars ğ•œ' : (E â†’L[ğ•œ] F) â†’ (E â†’L[ğ•œ'] F)) :=
  (isUniformEmbedding_restrictScalars ğ•œ').uniformContinuous

end UniformSpace

variable [TopologicalSpace F] [IsTopologicalAddGroup F] [Module ğ•œ F]
  (ğ•œ' : Type*) [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ' ğ•œ]
  [Module ğ•œ' E] [IsScalarTower ğ•œ' ğ•œ E] [Module ğ•œ' F] [IsScalarTower ğ•œ' ğ•œ F]

theorem isEmbedding_restrictScalars :
    IsEmbedding (restrictScalars ğ•œ' : (E â†’L[ğ•œ] F) â†’ (E â†’L[ğ•œ'] F)) :=
  letI : UniformSpace F := IsTopologicalAddGroup.toUniformSpace F
  haveI : IsUniformAddGroup F := isUniformAddGroup_of_addCommGroup
  (isUniformEmbedding_restrictScalars _).isEmbedding

@[continuity, fun_prop]
theorem continuous_restrictScalars :
    Continuous (restrictScalars ğ•œ' : (E â†’L[ğ•œ] F) â†’ (E â†’L[ğ•œ'] F)) :=
  (isEmbedding_restrictScalars _).continuous

variable (ğ•œ E F)
variable (ğ•œ'' : Type*) [Ring ğ•œ'']
  [Module ğ•œ'' F] [ContinuousConstSMul ğ•œ'' F] [SMulCommClass ğ•œ ğ•œ'' F] [SMulCommClass ğ•œ' ğ•œ'' F]

/-- `ContinuousLinearMap.restrictScalars` as a `ContinuousLinearMap`. -/
def restrictScalarsL : (E â†’L[ğ•œ] F) â†’L[ğ•œ''] E â†’L[ğ•œ'] F :=
  .mk <| restrictScalarsâ‚— ğ•œ E F ğ•œ' ğ•œ''

variable {ğ•œ E F ğ•œ' ğ•œ''}

@[simp]
theorem coe_restrictScalarsL : (restrictScalarsL ğ•œ E F ğ•œ' ğ•œ'' : (E â†’L[ğ•œ] F) â†’â‚—[ğ•œ''] E â†’L[ğ•œ'] F) =
    restrictScalarsâ‚— ğ•œ E F ğ•œ' ğ•œ'' :=
  rfl

@[simp]
theorem coe_restrict_scalarsL' : â‡‘(restrictScalarsL ğ•œ E F ğ•œ' ğ•œ'') = restrictScalars ğ•œ' :=
  rfl

end RestrictScalars

end ContinuousLinearMap

open ContinuousLinearMap

namespace ContinuousLinearEquiv

/-! ### Continuous linear equivalences -/

section Semilinear

variable {ğ•œ : Type*} {ğ•œâ‚‚ : Type*} {ğ•œâ‚ƒ : Type*} {ğ•œâ‚„ : Type*} {E : Type*} {F : Type*}
  {G : Type*} {H : Type*} [AddCommGroup E] [AddCommGroup F] [AddCommGroup G] [AddCommGroup H]
  [NormedField ğ•œ] [NormedField ğ•œâ‚‚] [NormedField ğ•œâ‚ƒ] [NormedField ğ•œâ‚„]
  [Module ğ•œ E] [Module ğ•œâ‚‚ F] [Module ğ•œâ‚ƒ G] [Module ğ•œâ‚„ H]
  [TopologicalSpace E] [TopologicalSpace F] [TopologicalSpace G] [TopologicalSpace H]
  [IsTopologicalAddGroup G] [IsTopologicalAddGroup H] [ContinuousConstSMul ğ•œâ‚ƒ G]
  [ContinuousConstSMul ğ•œâ‚„ H] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} {Ïƒâ‚‚â‚ : ğ•œâ‚‚ â†’+* ğ•œ} {Ïƒâ‚‚â‚ƒ : ğ•œâ‚‚ â†’+* ğ•œâ‚ƒ} {Ïƒâ‚â‚ƒ : ğ•œ â†’+* ğ•œâ‚ƒ}
  {Ïƒâ‚ƒâ‚„ : ğ•œâ‚ƒ â†’+* ğ•œâ‚„} {Ïƒâ‚„â‚ƒ : ğ•œâ‚„ â†’+* ğ•œâ‚ƒ} {Ïƒâ‚‚â‚„ : ğ•œâ‚‚ â†’+* ğ•œâ‚„} {Ïƒâ‚â‚„ : ğ•œ â†’+* ğ•œâ‚„} [RingHomInvPair Ïƒâ‚â‚‚ Ïƒâ‚‚â‚]
  [RingHomInvPair Ïƒâ‚‚â‚ Ïƒâ‚â‚‚] [RingHomInvPair Ïƒâ‚ƒâ‚„ Ïƒâ‚„â‚ƒ] [RingHomInvPair Ïƒâ‚„â‚ƒ Ïƒâ‚ƒâ‚„]
  [RingHomCompTriple Ïƒâ‚‚â‚ Ïƒâ‚â‚„ Ïƒâ‚‚â‚„] [RingHomCompTriple Ïƒâ‚‚â‚„ Ïƒâ‚„â‚ƒ Ïƒâ‚‚â‚ƒ] [RingHomCompTriple Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ Ïƒâ‚â‚ƒ]
  [RingHomCompTriple Ïƒâ‚â‚ƒ Ïƒâ‚ƒâ‚„ Ïƒâ‚â‚„] [RingHomCompTriple Ïƒâ‚‚â‚ƒ Ïƒâ‚ƒâ‚„ Ïƒâ‚‚â‚„] [RingHomCompTriple Ïƒâ‚â‚‚ Ïƒâ‚‚â‚„ Ïƒâ‚â‚„]
  [RingHomIsometric Ïƒâ‚â‚‚] [RingHomIsometric Ïƒâ‚‚â‚]

/-- A pair of continuous (semi)linear equivalences generates a (semi)linear equivalence between the
spaces of continuous (semi)linear maps. -/
@[simps apply symm_apply toLinearEquiv_apply toLinearEquiv_symm_apply]
def arrowCongrSL (eâ‚â‚‚ : E â‰ƒSL[Ïƒâ‚â‚‚] F) (eâ‚„â‚ƒ : H â‰ƒSL[Ïƒâ‚„â‚ƒ] G) :
    (E â†’SL[Ïƒâ‚â‚„] H) â‰ƒSL[Ïƒâ‚„â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G :=
{ eâ‚â‚‚.arrowCongrEquiv eâ‚„â‚ƒ with
    -- given explicitly to help `simps`
    toFun := fun L => (eâ‚„â‚ƒ : H â†’SL[Ïƒâ‚„â‚ƒ] G).comp (L.comp (eâ‚â‚‚.symm : F â†’SL[Ïƒâ‚‚â‚] E))
    -- given explicitly to help `simps`
    invFun := fun L => (eâ‚„â‚ƒ.symm : G â†’SL[Ïƒâ‚ƒâ‚„] H).comp (L.comp (eâ‚â‚‚ : E â†’SL[Ïƒâ‚â‚‚] F))
    map_add' := fun f g => by simp only [add_comp, comp_add]
    map_smul' := fun t f => by simp only [smul_comp, comp_smulâ‚›â‚—]
    continuous_toFun := ((postcomp F eâ‚„â‚ƒ.toContinuousLinearMap).comp
      (precomp H eâ‚â‚‚.symm.toContinuousLinearMap)).continuous
    continuous_invFun := ((precomp H eâ‚â‚‚.toContinuousLinearMap).comp
      (postcomp F eâ‚„â‚ƒ.symm.toContinuousLinearMap)).continuous }

end Semilinear

section Linear

variable {ğ•œ : Type*} {E : Type*} {F : Type*} {G : Type*} {H : Type*} [AddCommGroup E]
  [AddCommGroup F] [AddCommGroup G] [AddCommGroup H] [NormedField ğ•œ] [Module ğ•œ E]
  [Module ğ•œ F] [Module ğ•œ G] [Module ğ•œ H] [TopologicalSpace E] [TopologicalSpace F]
  [TopologicalSpace G] [TopologicalSpace H] [IsTopologicalAddGroup G] [IsTopologicalAddGroup H]
  [ContinuousConstSMul ğ•œ G] [ContinuousConstSMul ğ•œ H]

/-- A pair of continuous linear equivalences generates a continuous linear equivalence between
the spaces of continuous linear maps. -/
def arrowCongr (eâ‚ : E â‰ƒL[ğ•œ] F) (eâ‚‚ : H â‰ƒL[ğ•œ] G) : (E â†’L[ğ•œ] H) â‰ƒL[ğ•œ] F â†’L[ğ•œ] G :=
  eâ‚.arrowCongrSL eâ‚‚

@[simp] lemma arrowCongr_apply (eâ‚ : E â‰ƒL[ğ•œ] F) (eâ‚‚ : H â‰ƒL[ğ•œ] G) (f : E â†’L[ğ•œ] H) (x : F) :
    eâ‚.arrowCongr eâ‚‚ f x = eâ‚‚ (f (eâ‚.symm x)) := rfl

@[simp] lemma arrowCongr_symm (eâ‚ : E â‰ƒL[ğ•œ] F) (eâ‚‚ : H â‰ƒL[ğ•œ] G) :
    (eâ‚.arrowCongr eâ‚‚).symm = eâ‚.symm.arrowCongr eâ‚‚.symm := rfl

end Linear

end ContinuousLinearEquiv
