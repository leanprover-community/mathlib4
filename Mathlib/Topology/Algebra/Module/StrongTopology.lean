/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import Mathlib.Topology.Algebra.Module.UniformConvergence

#align_import topology.algebra.module.strong_topology from "leanprover-community/mathlib"@"8905e5ed90859939681a725b00f6063e65096d95"

/-!
# Strong topologies on the space of continuous linear maps

In this file, we define the strong topologies on `E â†’L[ğ•œ] F` associated with a family
`ğ”– : Set (Set E)` to be the topology of uniform convergence on the elements of `ğ”–` (also called
the topology of `ğ”–`-convergence).

The lemma `UniformOnFun.continuousSMul_of_image_bounded` tells us that this is a
vector space topology if the continuous linear image of any element of `ğ”–` is bounded (in the sense
of `Bornology.IsVonNBounded`).

We then declare an instance for the case where `ğ”–` is exactly the set of all bounded subsets of
`E`, giving us the so-called "topology of uniform convergence on bounded sets" (or "topology of
bounded convergence"), which coincides with the operator norm topology in the case of
`NormedSpace`s.

Other useful examples include the weak-* topology (when `ğ”–` is the set of finite sets or the set
of singletons) and the topology of compact convergence (when `ğ”–` is the set of relatively compact
sets).

## Main definitions

* `UniformConvergenceCLM` is a type synonym for `E â†’SL[Ïƒ] F` equipped with the `ğ”–`-topology.
* `UniformConvergenceCLM.instTopologicalSpace` is the topology mentioned above for an arbitrary `ğ”–`.
* `ContinuousLinearMap.topologicalSpace` is the topology of bounded convergence. This is
  declared as an instance.

## Main statements

* `UniformConvergenceCLM.instTopologicalAddGroup` and
  `UniformConvergenceCLM.instContinuousSMul` show that the strong topology
  makes `E â†’L[ğ•œ] F` a topological vector space, with the assumptions on `ğ”–` mentioned above.
* `ContinuousLinearMap.topologicalAddGroup` and
  `ContinuousLinearMap.continuousSMul` register these facts as instances for the special
  case of bounded convergence.

## References

* [N. Bourbaki, *Topological Vector Spaces*][bourbaki1987]

## TODO

* Add convergence on compact subsets

## Tags

uniform convergence, bounded convergence
-/


open scoped Topology UniformConvergence

section General

/-! ### ğ”–-Topologies -/

variable {ğ•œâ‚ ğ•œâ‚‚ : Type*} [NormedField ğ•œâ‚] [NormedField ğ•œâ‚‚] (Ïƒ : ğ•œâ‚ â†’+* ğ•œâ‚‚) {E E' F F' : Type*}
  [AddCommGroup E] [Module ğ•œâ‚ E] [AddCommGroup E'] [Module â„ E'] [AddCommGroup F] [Module ğ•œâ‚‚ F]
  [AddCommGroup F'] [Module â„ F'] [TopologicalSpace E] [TopologicalSpace E'] (F)

/-- Given `E` and `F` two topological vector spaces and `ğ”– : Set (Set E)`, then
`UniformConvergenceCLM Ïƒ F ğ”–` is a type synonym of `E â†’SL[Ïƒ] F` equipped with the "topology of
uniform convergence on the elements of `ğ”–`".

If the continuous linear image of any element of `ğ”–` is bounded, this makes `E â†’SL[Ïƒ] F` a
topological vector space. -/
@[nolint unusedArguments]
def UniformConvergenceCLM [TopologicalSpace F] [TopologicalAddGroup F] (_ : Set (Set E)) :=
  E â†’SL[Ïƒ] F

namespace UniformConvergenceCLM

instance instFunLike [TopologicalSpace F] [TopologicalAddGroup F]
    (ğ”– : Set (Set E)) : FunLike (UniformConvergenceCLM Ïƒ F ğ”–) E F :=
  ContinuousLinearMap.funLike

instance instContinuousSemilinearMapClass [TopologicalSpace F] [TopologicalAddGroup F]
    (ğ”– : Set (Set E)) : ContinuousSemilinearMapClass (UniformConvergenceCLM Ïƒ F ğ”–) Ïƒ E F :=
  ContinuousLinearMap.continuousSemilinearMapClass

instance instTopologicalSpace [TopologicalSpace F] [TopologicalAddGroup F] (ğ”– : Set (Set E)) :
    TopologicalSpace (UniformConvergenceCLM Ïƒ F ğ”–) :=
  (@UniformOnFun.topologicalSpace E F (TopologicalAddGroup.toUniformSpace F) ğ”–).induced
    (DFunLike.coe : (UniformConvergenceCLM Ïƒ F ğ”–) â†’ (E â†’áµ¤[ğ”–] F))
#align continuous_linear_map.strong_topology UniformConvergenceCLM.instTopologicalSpace

theorem topologicalSpace_eq [UniformSpace F] [UniformAddGroup F] (ğ”– : Set (Set E)) :
    instTopologicalSpace Ïƒ F ğ”– = TopologicalSpace.induced DFunLike.coe
      (UniformOnFun.topologicalSpace E F ğ”–) := by
  rw [instTopologicalSpace]
  congr
  exact UniformAddGroup.toUniformSpace_eq

/-- The uniform structure associated with `ContinuousLinearMap.strongTopology`. We make sure
that this has nice definitional properties. -/
instance instUniformSpace [UniformSpace F] [UniformAddGroup F]
    (ğ”– : Set (Set E)) : UniformSpace (UniformConvergenceCLM Ïƒ F ğ”–) :=
  UniformSpace.replaceTopology
    ((UniformOnFun.uniformSpace E F ğ”–).comap
      (DFunLike.coe : (UniformConvergenceCLM Ïƒ F ğ”–) â†’ (E â†’áµ¤[ğ”–] F)))
    (by rw [UniformConvergenceCLM.instTopologicalSpace, UniformAddGroup.toUniformSpace_eq]; rfl)
#align continuous_linear_map.strong_uniformity UniformConvergenceCLM.instUniformSpace

theorem uniformSpace_eq [UniformSpace F] [UniformAddGroup F] (ğ”– : Set (Set E)) :
    instUniformSpace Ïƒ F ğ”– = UniformSpace.comap DFunLike.coe (UniformOnFun.uniformSpace E F ğ”–) := by
  rw [instUniformSpace, UniformSpace.replaceTopology_eq]

@[simp]
theorem uniformity_toTopologicalSpace_eq [UniformSpace F] [UniformAddGroup F] (ğ”– : Set (Set E)) :
    (UniformConvergenceCLM.instUniformSpace Ïƒ F ğ”–).toTopologicalSpace =
      UniformConvergenceCLM.instTopologicalSpace Ïƒ F ğ”– :=
  rfl
#align continuous_linear_map.strong_uniformity_topology_eq UniformConvergenceCLM.uniformity_toTopologicalSpace_eq

theorem uniformEmbedding_coeFn [UniformSpace F] [UniformAddGroup F] (ğ”– : Set (Set E)) :
    UniformEmbedding (Î± := UniformConvergenceCLM Ïƒ F ğ”–) (Î² := E â†’áµ¤[ğ”–] F) DFunLike.coe :=
  âŸ¨âŸ¨rflâŸ©, DFunLike.coe_injectiveâŸ©
#align continuous_linear_map.strong_uniformity.uniform_embedding_coe_fn UniformConvergenceCLM.uniformEmbedding_coeFn

theorem embedding_coeFn [UniformSpace F] [UniformAddGroup F] (ğ”– : Set (Set E)) :
    Embedding (X := UniformConvergenceCLM Ïƒ F ğ”–) (Y := E â†’áµ¤[ğ”–] F)
      (UniformOnFun.ofFun ğ”– âˆ˜ DFunLike.coe) :=
  UniformEmbedding.embedding (uniformEmbedding_coeFn _ _ _)
#align continuous_linear_map.strong_topology.embedding_coe_fn UniformConvergenceCLM.embedding_coeFn

instance instAddCommGroup [TopologicalSpace F] [TopologicalAddGroup F] (ğ”– : Set (Set E)) :
    AddCommGroup (UniformConvergenceCLM Ïƒ F ğ”–) := ContinuousLinearMap.addCommGroup

instance instUniformAddGroup [UniformSpace F] [UniformAddGroup F] (ğ”– : Set (Set E)) :
    UniformAddGroup (UniformConvergenceCLM Ïƒ F ğ”–) := by
  let Ï† : (UniformConvergenceCLM Ïƒ F ğ”–) â†’+ E â†’áµ¤[ğ”–] F :=
    âŸ¨âŸ¨(DFunLike.coe : (UniformConvergenceCLM Ïƒ F ğ”–) â†’ E â†’áµ¤[ğ”–] F), rflâŸ©, fun _ _ => rflâŸ©
  exact (uniformEmbedding_coeFn _ _ _).uniformAddGroup Ï†
#align continuous_linear_map.strong_uniformity.uniform_add_group UniformConvergenceCLM.instUniformAddGroup

instance instTopologicalAddGroup [TopologicalSpace F] [TopologicalAddGroup F]
    (ğ”– : Set (Set E)) : TopologicalAddGroup (UniformConvergenceCLM Ïƒ F ğ”–) := by
  letI : UniformSpace F := TopologicalAddGroup.toUniformSpace F
  haveI : UniformAddGroup F := comm_topologicalAddGroup_is_uniform
  infer_instance
#align continuous_linear_map.strong_topology.topological_add_group UniformConvergenceCLM.instTopologicalAddGroup

theorem t2Space [TopologicalSpace F] [TopologicalAddGroup F] [T2Space F]
    (ğ”– : Set (Set E)) (hğ”– : â‹ƒâ‚€ ğ”– = Set.univ) : T2Space (UniformConvergenceCLM Ïƒ F ğ”–) := by
  letI : UniformSpace F := TopologicalAddGroup.toUniformSpace F
  haveI : UniformAddGroup F := comm_topologicalAddGroup_is_uniform
  haveI : T2Space (E â†’áµ¤[ğ”–] F) := UniformOnFun.t2Space_of_covering hğ”–
  exact (embedding_coeFn Ïƒ F ğ”–).t2Space
#align continuous_linear_map.strong_topology.t2_space UniformConvergenceCLM.t2Space

instance instDistribMulAction (M : Type*) [Monoid M] [DistribMulAction M F] [SMulCommClass ğ•œâ‚‚ M F]
    [TopologicalSpace F] [TopologicalAddGroup F] [ContinuousConstSMul M F] (ğ”– : Set (Set E)) :
    DistribMulAction M (UniformConvergenceCLM Ïƒ F ğ”–) := ContinuousLinearMap.distribMulAction

instance instModule (R : Type*) [Semiring R] [Module R F] [SMulCommClass ğ•œâ‚‚ R F]
    [TopologicalSpace F] [ContinuousConstSMul R F] [TopologicalAddGroup F] (ğ”– : Set (Set E)) :
    Module R (UniformConvergenceCLM Ïƒ F ğ”–) := ContinuousLinearMap.module

theorem continuousSMul [RingHomSurjective Ïƒ] [RingHomIsometric Ïƒ]
    [TopologicalSpace F] [TopologicalAddGroup F] [ContinuousSMul ğ•œâ‚‚ F] (ğ”– : Set (Set E))
    (hğ”–â‚ƒ : âˆ€ S âˆˆ ğ”–, Bornology.IsVonNBounded ğ•œâ‚ S) :
    ContinuousSMul ğ•œâ‚‚ (UniformConvergenceCLM Ïƒ F ğ”–) := by
  letI : UniformSpace F := TopologicalAddGroup.toUniformSpace F
  haveI : UniformAddGroup F := comm_topologicalAddGroup_is_uniform
  let Ï† : (UniformConvergenceCLM Ïƒ F ğ”–) â†’â‚—[ğ•œâ‚‚] E â†’ F :=
    âŸ¨âŸ¨DFunLike.coe, fun _ _ => rflâŸ©, fun _ _ => rflâŸ©
  exact UniformOnFun.continuousSMul_induced_of_image_bounded ğ•œâ‚‚ E F (UniformConvergenceCLM Ïƒ F ğ”–) Ï†
    âŸ¨rflâŸ© fun u s hs => (hğ”–â‚ƒ s hs).image u
#align continuous_linear_map.strong_topology.has_continuous_smul UniformConvergenceCLM.continuousSMul

theorem hasBasis_nhds_zero_of_basis [TopologicalSpace F] [TopologicalAddGroup F]
    {Î¹ : Type*} (ğ”– : Set (Set E)) (hğ”–â‚ : ğ”–.Nonempty) (hğ”–â‚‚ : DirectedOn (Â· âŠ† Â·) ğ”–) {p : Î¹ â†’ Prop}
    {b : Î¹ â†’ Set F} (h : (ğ“ 0 : Filter F).HasBasis p b) :
    (ğ“ (0 : UniformConvergenceCLM Ïƒ F ğ”–)).HasBasis
      (fun Si : Set E Ã— Î¹ => Si.1 âˆˆ ğ”– âˆ§ p Si.2)
      fun Si => { f : E â†’SL[Ïƒ] F | âˆ€ x âˆˆ Si.1, f x âˆˆ b Si.2 } := by
  letI : UniformSpace F := TopologicalAddGroup.toUniformSpace F
  haveI : UniformAddGroup F := comm_topologicalAddGroup_is_uniform
  rw [(embedding_coeFn Ïƒ F ğ”–).toInducing.nhds_eq_comap]
  exact (UniformOnFun.hasBasis_nhds_zero_of_basis ğ”– hğ”–â‚ hğ”–â‚‚ h).comap DFunLike.coe
#align continuous_linear_map.strong_topology.has_basis_nhds_zero_of_basis UniformConvergenceCLM.hasBasis_nhds_zero_of_basis

theorem hasBasis_nhds_zero [TopologicalSpace F] [TopologicalAddGroup F]
    (ğ”– : Set (Set E)) (hğ”–â‚ : ğ”–.Nonempty) (hğ”–â‚‚ : DirectedOn (Â· âŠ† Â·) ğ”–) :
    (ğ“ (0 : UniformConvergenceCLM Ïƒ F ğ”–)).HasBasis
      (fun SV : Set E Ã— Set F => SV.1 âˆˆ ğ”– âˆ§ SV.2 âˆˆ (ğ“ 0 : Filter F)) fun SV =>
      { f : UniformConvergenceCLM Ïƒ F ğ”– | âˆ€ x âˆˆ SV.1, f x âˆˆ SV.2 } :=
  hasBasis_nhds_zero_of_basis Ïƒ F ğ”– hğ”–â‚ hğ”–â‚‚ (ğ“ 0).basis_sets
#align continuous_linear_map.strong_topology.has_basis_nhds_zero UniformConvergenceCLM.hasBasis_nhds_zero

instance instUniformContinuousConstSMul (M : Type*)
    [Monoid M] [DistribMulAction M F] [SMulCommClass ğ•œâ‚‚ M F]
    [UniformSpace F] [UniformAddGroup F] [UniformContinuousConstSMul M F] (ğ”– : Set (Set E)) :
    UniformContinuousConstSMul M (UniformConvergenceCLM Ïƒ F ğ”–) :=
  (uniformEmbedding_coeFn Ïƒ F ğ”–).toUniformInducing.uniformContinuousConstSMul fun _ _ â†¦ by rfl

instance instContinuousConstSMul (M : Type*)
    [Monoid M] [DistribMulAction M F] [SMulCommClass ğ•œâ‚‚ M F]
    [TopologicalSpace F] [TopologicalAddGroup F] [ContinuousConstSMul M F] (ğ”– : Set (Set E)) :
    ContinuousConstSMul M (UniformConvergenceCLM Ïƒ F ğ”–) :=
  let _ := TopologicalAddGroup.toUniformSpace F
  have _ : UniformAddGroup F := comm_topologicalAddGroup_is_uniform
  have _ := uniformContinuousConstSMul_of_continuousConstSMul M F
  inferInstance

theorem tendsto_iff_tendstoUniformlyOn {Î¹ : Type*} {p : Filter Î¹} [UniformSpace F]
    [UniformAddGroup F] (ğ”– : Set (Set E)) {a : Î¹ â†’ UniformConvergenceCLM Ïƒ F ğ”–}
    {aâ‚€ : UniformConvergenceCLM Ïƒ F ğ”–} :
    Filter.Tendsto a p (ğ“ aâ‚€) â†” âˆ€ s âˆˆ ğ”–, TendstoUniformlyOn (a Â· Â·) aâ‚€ p s := by
  rw [(embedding_coeFn Ïƒ F ğ”–).tendsto_nhds_iff, UniformOnFun.tendsto_iff_tendstoUniformlyOn]
  rfl

variable {ğ”–â‚ ğ”–â‚‚ : Set (Set E)}

theorem uniformSpace_mono [UniformSpace F] [UniformAddGroup F] (h : ğ”–â‚‚ âŠ† ğ”–â‚) :
    instUniformSpace Ïƒ F ğ”–â‚ â‰¤ instUniformSpace Ïƒ F ğ”–â‚‚ := by
  simp_rw [uniformSpace_eq]
  exact UniformSpace.comap_mono (UniformOnFun.mono (le_refl _) h)

theorem topologicalSpace_mono [TopologicalSpace F] [TopologicalAddGroup F] (h : ğ”–â‚‚ âŠ† ğ”–â‚) :
    instTopologicalSpace Ïƒ F ğ”–â‚ â‰¤ instTopologicalSpace Ïƒ F ğ”–â‚‚ := by
  letI := TopologicalAddGroup.toUniformSpace F
  haveI : UniformAddGroup F := comm_topologicalAddGroup_is_uniform
  simp_rw [â† uniformity_toTopologicalSpace_eq]
  exact UniformSpace.toTopologicalSpace_mono (uniformSpace_mono Ïƒ F h)

end UniformConvergenceCLM

end General

namespace ContinuousLinearMap

section BoundedSets

/-! ### Topology of bounded convergence  -/

variable {ğ•œâ‚ ğ•œâ‚‚ ğ•œâ‚ƒ : Type*} [NormedField ğ•œâ‚] [NormedField ğ•œâ‚‚] [NormedField ğ•œâ‚ƒ] {Ïƒ : ğ•œâ‚ â†’+* ğ•œâ‚‚}
  {Ï„ : ğ•œâ‚‚ â†’+* ğ•œâ‚ƒ} {Ï : ğ•œâ‚ â†’+* ğ•œâ‚ƒ} [RingHomCompTriple Ïƒ Ï„ Ï] {E E' F F' G : Type*} [AddCommGroup E]
  [Module ğ•œâ‚ E] [AddCommGroup E'] [Module â„ E'] [AddCommGroup F] [Module ğ•œâ‚‚ F] [AddCommGroup F']
  [Module â„ F'] [AddCommGroup G] [Module ğ•œâ‚ƒ G] [TopologicalSpace E]

/-- The topology of bounded convergence on `E â†’L[ğ•œ] F`. This coincides with the topology induced by
the operator norm when `E` and `F` are normed spaces. -/
instance topologicalSpace [TopologicalSpace F] [TopologicalAddGroup F] :
    TopologicalSpace (E â†’SL[Ïƒ] F) :=
  UniformConvergenceCLM.instTopologicalSpace Ïƒ F { S | Bornology.IsVonNBounded ğ•œâ‚ S }

instance topologicalAddGroup [TopologicalSpace F] [TopologicalAddGroup F] :
    TopologicalAddGroup (E â†’SL[Ïƒ] F) :=
  UniformConvergenceCLM.instTopologicalAddGroup Ïƒ F _

instance continuousSMul [RingHomSurjective Ïƒ] [RingHomIsometric Ïƒ] [TopologicalSpace F]
    [TopologicalAddGroup F] [ContinuousSMul ğ•œâ‚‚ F] : ContinuousSMul ğ•œâ‚‚ (E â†’SL[Ïƒ] F) :=
  UniformConvergenceCLM.continuousSMul Ïƒ F { S | Bornology.IsVonNBounded ğ•œâ‚ S } fun _ hs => hs

instance uniformSpace [UniformSpace F] [UniformAddGroup F] : UniformSpace (E â†’SL[Ïƒ] F) :=
  UniformConvergenceCLM.instUniformSpace Ïƒ F { S | Bornology.IsVonNBounded ğ•œâ‚ S }

instance uniformAddGroup [UniformSpace F] [UniformAddGroup F] : UniformAddGroup (E â†’SL[Ïƒ] F) :=
  UniformConvergenceCLM.instUniformAddGroup Ïƒ F _

instance [TopologicalSpace F] [TopologicalAddGroup F] [ContinuousSMul ğ•œâ‚ E] [T2Space F] :
    T2Space (E â†’SL[Ïƒ] F) :=
  UniformConvergenceCLM.t2Space Ïƒ F _
    (Set.eq_univ_of_forall fun x =>
      Set.mem_sUnion_of_mem (Set.mem_singleton x) (Bornology.isVonNBounded_singleton x))

protected theorem hasBasis_nhds_zero_of_basis [TopologicalSpace F] [TopologicalAddGroup F]
    {Î¹ : Type*} {p : Î¹ â†’ Prop} {b : Î¹ â†’ Set F} (h : (ğ“ 0 : Filter F).HasBasis p b) :
    (ğ“ (0 : E â†’SL[Ïƒ] F)).HasBasis (fun Si : Set E Ã— Î¹ => Bornology.IsVonNBounded ğ•œâ‚ Si.1 âˆ§ p Si.2)
      fun Si => { f : E â†’SL[Ïƒ] F | âˆ€ x âˆˆ Si.1, f x âˆˆ b Si.2 } :=
  UniformConvergenceCLM.hasBasis_nhds_zero_of_basis Ïƒ F { S | Bornology.IsVonNBounded ğ•œâ‚ S }
    âŸ¨âˆ…, Bornology.isVonNBounded_empty ğ•œâ‚ EâŸ©
    (directedOn_of_sup_mem fun _ _ => Bornology.IsVonNBounded.union) h
#align continuous_linear_map.has_basis_nhds_zero_of_basis ContinuousLinearMap.hasBasis_nhds_zero_of_basis

protected theorem hasBasis_nhds_zero [TopologicalSpace F] [TopologicalAddGroup F] :
    (ğ“ (0 : E â†’SL[Ïƒ] F)).HasBasis
      (fun SV : Set E Ã— Set F => Bornology.IsVonNBounded ğ•œâ‚ SV.1 âˆ§ SV.2 âˆˆ (ğ“ 0 : Filter F))
      fun SV => { f : E â†’SL[Ïƒ] F | âˆ€ x âˆˆ SV.1, f x âˆˆ SV.2 } :=
  ContinuousLinearMap.hasBasis_nhds_zero_of_basis (ğ“ 0).basis_sets
#align continuous_linear_map.has_basis_nhds_zero ContinuousLinearMap.hasBasis_nhds_zero

instance uniformContinuousConstSMul
    {M : Type*} [Monoid M] [DistribMulAction M F] [SMulCommClass ğ•œâ‚‚ M F]
    [UniformSpace F] [UniformAddGroup F] [UniformContinuousConstSMul M F] :
    UniformContinuousConstSMul M (E â†’SL[Ïƒ] F) :=
  UniformConvergenceCLM.instUniformContinuousConstSMul Ïƒ F _ _

instance continuousConstSMul {M : Type*} [Monoid M] [DistribMulAction M F] [SMulCommClass ğ•œâ‚‚ M F]
    [TopologicalSpace F] [TopologicalAddGroup F] [ContinuousConstSMul M F] :
    ContinuousConstSMul M (E â†’SL[Ïƒ] F) :=
  UniformConvergenceCLM.instContinuousConstSMul Ïƒ F _ _

variable (G) [TopologicalSpace F] [TopologicalSpace G]

/-- Pre-composition by a *fixed* continuous linear map as a continuous linear map.
Note that in non-normed space it is not always true that composition is continuous
in both variables, so we have to fix one of them. -/
@[simps]
def precomp [TopologicalAddGroup G] [ContinuousConstSMul ğ•œâ‚ƒ G] [RingHomSurjective Ïƒ]
    [RingHomIsometric Ïƒ] (L : E â†’SL[Ïƒ] F) : (F â†’SL[Ï„] G) â†’L[ğ•œâ‚ƒ] E â†’SL[Ï] G where
  toFun f := f.comp L
  map_add' f g := add_comp f g L
  map_smul' a f := smul_comp a f L
  cont := by
    letI : UniformSpace G := TopologicalAddGroup.toUniformSpace G
    haveI : UniformAddGroup G := comm_topologicalAddGroup_is_uniform
    rw [(UniformConvergenceCLM.embedding_coeFn _ _ _).continuous_iff]
    -- Porting note: without this, the following doesn't work
    change Continuous ((fun f â†¦ UniformOnFun.ofFun _ (f âˆ˜ L)) âˆ˜ DFunLike.coe)
    exact (UniformOnFun.precomp_uniformContinuous fun S hS => hS.image L).continuous.comp
        (UniformConvergenceCLM.embedding_coeFn _ _ _).continuous
#align continuous_linear_map.precomp ContinuousLinearMap.precomp

variable (E) {G}

/-- Post-composition by a *fixed* continuous linear map as a continuous linear map.
Note that in non-normed space it is not always true that composition is continuous
in both variables, so we have to fix one of them. -/
@[simps]
def postcomp [TopologicalAddGroup F] [TopologicalAddGroup G] [ContinuousConstSMul ğ•œâ‚ƒ G]
    [ContinuousConstSMul ğ•œâ‚‚ F] (L : F â†’SL[Ï„] G) : (E â†’SL[Ïƒ] F) â†’SL[Ï„] E â†’SL[Ï] G where
  toFun f := L.comp f
  map_add' := comp_add L
  map_smul' := comp_smulâ‚›â‚— L
  cont := by
    letI : UniformSpace G := TopologicalAddGroup.toUniformSpace G
    haveI : UniformAddGroup G := comm_topologicalAddGroup_is_uniform
    letI : UniformSpace F := TopologicalAddGroup.toUniformSpace F
    haveI : UniformAddGroup F := comm_topologicalAddGroup_is_uniform
    rw [(UniformConvergenceCLM.embedding_coeFn _ _ _).continuous_iff]
    exact
      (UniformOnFun.postcomp_uniformContinuous L.uniformContinuous).continuous.comp
        (UniformConvergenceCLM.embedding_coeFn _ _ _).continuous
#align continuous_linear_map.postcomp ContinuousLinearMap.postcomp

end BoundedSets

section BilinearMaps

variable {ğ•œ : Type*} [NormedField ğ•œ] {E F G : Type*}
  [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace E]
  [AddCommGroup F] [Module ğ•œ F] [TopologicalSpace F]
  [AddCommGroup G] [Module ğ•œ G]
  [TopologicalSpace G] [TopologicalAddGroup G] [ContinuousConstSMul ğ•œ G]

/-- Send a continuous bilinear map to an abstract bilinear map (forgetting continuity). -/
def toLinearMapâ‚‚ (L : E â†’L[ğ•œ] F â†’L[ğ•œ] G) : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] G := (coeLM ğ•œ).comp L.toLinearMap

@[simp] lemma toLinearMapâ‚‚_apply (L : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (v : E) (w : F) :
    L.toLinearMapâ‚‚ v w = L v w := rfl

end BilinearMaps

end ContinuousLinearMap

open ContinuousLinearMap

namespace ContinuousLinearEquiv

/-! ### Continuous linear equivalences -/

section Semilinear

variable {ğ•œ : Type*} {ğ•œâ‚‚ : Type*} {ğ•œâ‚ƒ : Type*} {ğ•œâ‚„ : Type*} {E : Type*} {F : Type*}
  {G : Type*} {H : Type*} [AddCommGroup E] [AddCommGroup F] [AddCommGroup G] [AddCommGroup H]
  [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] [NontriviallyNormedField ğ•œâ‚ƒ]
  [NontriviallyNormedField ğ•œâ‚„] [Module ğ•œ E] [Module ğ•œâ‚‚ F] [Module ğ•œâ‚ƒ G] [Module ğ•œâ‚„ H]
  [TopologicalSpace E] [TopologicalSpace F] [TopologicalSpace G] [TopologicalSpace H]
  [TopologicalAddGroup G] [TopologicalAddGroup H] [ContinuousConstSMul ğ•œâ‚ƒ G]
  [ContinuousConstSMul ğ•œâ‚„ H] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} {Ïƒâ‚‚â‚ : ğ•œâ‚‚ â†’+* ğ•œ} {Ïƒâ‚‚â‚ƒ : ğ•œâ‚‚ â†’+* ğ•œâ‚ƒ} {Ïƒâ‚â‚ƒ : ğ•œ â†’+* ğ•œâ‚ƒ}
  {Ïƒâ‚ƒâ‚„ : ğ•œâ‚ƒ â†’+* ğ•œâ‚„} {Ïƒâ‚„â‚ƒ : ğ•œâ‚„ â†’+* ğ•œâ‚ƒ} {Ïƒâ‚‚â‚„ : ğ•œâ‚‚ â†’+* ğ•œâ‚„} {Ïƒâ‚â‚„ : ğ•œ â†’+* ğ•œâ‚„} [RingHomInvPair Ïƒâ‚â‚‚ Ïƒâ‚‚â‚]
  [RingHomInvPair Ïƒâ‚‚â‚ Ïƒâ‚â‚‚] [RingHomInvPair Ïƒâ‚ƒâ‚„ Ïƒâ‚„â‚ƒ] [RingHomInvPair Ïƒâ‚„â‚ƒ Ïƒâ‚ƒâ‚„]
  [RingHomCompTriple Ïƒâ‚‚â‚ Ïƒâ‚â‚„ Ïƒâ‚‚â‚„] [RingHomCompTriple Ïƒâ‚‚â‚„ Ïƒâ‚„â‚ƒ Ïƒâ‚‚â‚ƒ] [RingHomCompTriple Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ Ïƒâ‚â‚ƒ]
  [RingHomCompTriple Ïƒâ‚â‚ƒ Ïƒâ‚ƒâ‚„ Ïƒâ‚â‚„] [RingHomCompTriple Ïƒâ‚‚â‚ƒ Ïƒâ‚ƒâ‚„ Ïƒâ‚‚â‚„] [RingHomCompTriple Ïƒâ‚â‚‚ Ïƒâ‚‚â‚„ Ïƒâ‚â‚„]
  [RingHomIsometric Ïƒâ‚â‚‚] [RingHomIsometric Ïƒâ‚‚â‚]

/-- A pair of continuous (semi)linear equivalences generates a (semi)linear equivalence between the
spaces of continuous (semi)linear maps. -/
@[simps]
def arrowCongrSL (eâ‚â‚‚ : E â‰ƒSL[Ïƒâ‚â‚‚] F) (eâ‚„â‚ƒ : H â‰ƒSL[Ïƒâ‚„â‚ƒ] G) :
    (E â†’SL[Ïƒâ‚â‚„] H) â‰ƒSL[Ïƒâ‚„â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G :=
{ eâ‚â‚‚.arrowCongrEquiv eâ‚„â‚ƒ with
    -- given explicitly to help `simps`
    toFun := fun L => (eâ‚„â‚ƒ : H â†’SL[Ïƒâ‚„â‚ƒ] G).comp (L.comp (eâ‚â‚‚.symm : F â†’SL[Ïƒâ‚‚â‚] E))
    -- given explicitly to help `simps`
    invFun := fun L => (eâ‚„â‚ƒ.symm : G â†’SL[Ïƒâ‚ƒâ‚„] H).comp (L.comp (eâ‚â‚‚ : E â†’SL[Ïƒâ‚â‚‚] F))
    map_add' := fun f g => by simp only [add_comp, comp_add]
    map_smul' := fun t f => by simp only [smul_comp, comp_smulâ‚›â‚—]
    continuous_toFun := ((postcomp F eâ‚„â‚ƒ.toContinuousLinearMap).comp
      (precomp H eâ‚â‚‚.symm.toContinuousLinearMap)).continuous
    continuous_invFun := ((precomp H eâ‚â‚‚.toContinuousLinearMap).comp
      (postcomp F eâ‚„â‚ƒ.symm.toContinuousLinearMap)).continuous }
set_option linter.uppercaseLean3 false in
#align continuous_linear_equiv.arrow_congrSL ContinuousLinearEquiv.arrowCongrSL
set_option linter.uppercaseLean3 false in
#align continuous_linear_equiv.arrow_congrSL_apply ContinuousLinearEquiv.arrowCongrSL_apply
set_option linter.uppercaseLean3 false in
#align continuous_linear_equiv.arrow_congrSL_symm_apply ContinuousLinearEquiv.arrowCongrSL_symm_apply

-- Porting note: the following two lemmas were autogenerated by `simps` in Lean3, but this is
-- no longer the case. The first one can already be proven by `simp`, but the second can't.

theorem arrowCongrSL_toLinearEquiv_apply (eâ‚â‚‚ : E â‰ƒSL[Ïƒâ‚â‚‚] F) (eâ‚„â‚ƒ : H â‰ƒSL[Ïƒâ‚„â‚ƒ] G)
    (L : E â†’SL[Ïƒâ‚â‚„] H) : (eâ‚â‚‚.arrowCongrSL eâ‚„â‚ƒ).toLinearEquiv L =
      (eâ‚„â‚ƒ : H â†’SL[Ïƒâ‚„â‚ƒ] G).comp (L.comp (eâ‚â‚‚.symm : F â†’SL[Ïƒâ‚‚â‚] E)) :=
  rfl
set_option linter.uppercaseLean3 false in
#align continuous_linear_equiv.arrow_congrSL_to_linear_equiv_apply ContinuousLinearEquiv.arrowCongrSL_toLinearEquiv_apply

@[simp]
theorem arrowCongrSL_toLinearEquiv_symm_apply (eâ‚â‚‚ : E â‰ƒSL[Ïƒâ‚â‚‚] F) (eâ‚„â‚ƒ : H â‰ƒSL[Ïƒâ‚„â‚ƒ] G)
    (L : F â†’SL[Ïƒâ‚‚â‚ƒ] G) : (eâ‚â‚‚.arrowCongrSL eâ‚„â‚ƒ).toLinearEquiv.symm L =
      (eâ‚„â‚ƒ.symm : G â†’SL[Ïƒâ‚ƒâ‚„] H).comp (L.comp (eâ‚â‚‚ : E â†’SL[Ïƒâ‚â‚‚] F)) :=
  rfl
set_option linter.uppercaseLean3 false in
#align continuous_linear_equiv.arrow_congrSL_to_linear_equiv_symm_apply ContinuousLinearEquiv.arrowCongrSL_toLinearEquiv_symm_apply

end Semilinear

section Linear

variable {ğ•œ : Type*} {E : Type*} {F : Type*} {G : Type*} {H : Type*} [AddCommGroup E]
  [AddCommGroup F] [AddCommGroup G] [AddCommGroup H] [NontriviallyNormedField ğ•œ] [Module ğ•œ E]
  [Module ğ•œ F] [Module ğ•œ G] [Module ğ•œ H] [TopologicalSpace E] [TopologicalSpace F]
  [TopologicalSpace G] [TopologicalSpace H] [TopologicalAddGroup G] [TopologicalAddGroup H]
  [ContinuousConstSMul ğ•œ G] [ContinuousConstSMul ğ•œ H]

/-- A pair of continuous linear equivalences generates a continuous linear equivalence between
the spaces of continuous linear maps. -/
def arrowCongr (eâ‚ : E â‰ƒL[ğ•œ] F) (eâ‚‚ : H â‰ƒL[ğ•œ] G) : (E â†’L[ğ•œ] H) â‰ƒL[ğ•œ] F â†’L[ğ•œ] G :=
  eâ‚.arrowCongrSL eâ‚‚
#align continuous_linear_equiv.arrow_congr ContinuousLinearEquiv.arrowCongr

@[simp] lemma arrowCongr_apply (eâ‚ : E â‰ƒL[ğ•œ] F) (eâ‚‚ : H â‰ƒL[ğ•œ] G) (f : E â†’L[ğ•œ] H) (x : F) :
    eâ‚.arrowCongr eâ‚‚ f x = eâ‚‚ (f (eâ‚.symm x)) := rfl

@[simp] lemma arrowCongr_symm (eâ‚ : E â‰ƒL[ğ•œ] F) (eâ‚‚ : H â‰ƒL[ğ•œ] G) :
    (eâ‚.arrowCongr eâ‚‚).symm = eâ‚.symm.arrowCongr eâ‚‚.symm := rfl

end Linear

end ContinuousLinearEquiv
