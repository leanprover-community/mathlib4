/-
Copyright (c) 2025 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
module

public import Mathlib.Topology.Algebra.Module.LinearMap
public import Mathlib.Topology.Sets.Closeds

/-!
# Closed submodules of a topological module

This file builds the frame of closed `R`-submodules of a topological module `M`.

One can turn `s : Submodule R E` + `hs : IsClosed s` into `s : ClosedSubmodule R E` in a tactic
block by doing `lift s to ClosedSubmodule R E using hs`.

## TODO

Actually provide the `Order.Frame (ClosedSubmodule R M)` instance.
-/

@[expose] public section

open Function Order TopologicalSpace

variable {Î¹ : Sort*} {R M N O : Type*} [Semiring R]
  [AddCommMonoid M] [TopologicalSpace M] [Module R M]
  [AddCommMonoid N] [TopologicalSpace N] [Module R N]
  [AddCommMonoid O] [TopologicalSpace O] [Module R O]

variable (R M) in
/-- The type of closed submodules of a topological module. -/
@[ext]
structure ClosedSubmodule extends Submodule R M, Closeds M where

namespace ClosedSubmodule
variable {s t : ClosedSubmodule R M} {x : M}

attribute [coe] toSubmodule toCloseds

/-- Reinterpret a closed submodule as a submodule. -/
add_decl_doc toSubmodule

/-- Reinterpret a closed submodule as a closed set. -/
add_decl_doc toCloseds

lemma toSubmodule_injective : Injective (toSubmodule : ClosedSubmodule R M â†’ Submodule R M) :=
  fun s t h â†¦ by cases s; congr!

instance : SetLike (ClosedSubmodule R M) M where
  coe s := s.1
  coe_injective' _ _ h := toSubmodule_injective <| SetLike.coe_injective h

lemma toCloseds_injective : Injective (toCloseds : ClosedSubmodule R M â†’ Closeds M) :=
  fun _s _t h â†¦ SetLike.coe_injective congr(($h : Set M))

instance : AddSubmonoidClass (ClosedSubmodule R M) M where
  zero_mem s := s.zero_mem
  add_mem {s} := s.add_mem

instance : SMulMemClass (ClosedSubmodule R M) R M where
  smul_mem {s} r := s.smul_mem r

instance : Coe (ClosedSubmodule R M) (Submodule R M) where
  coe := toSubmodule

@[simp] lemma carrier_eq_coe (s : ClosedSubmodule R M) : s.carrier = s := rfl

@[simp] lemma mem_mk {s : Submodule R M} {hs} : x âˆˆ mk s hs â†” x âˆˆ s := .rfl

@[simp, norm_cast]
lemma coe_toSubmodule (s : ClosedSubmodule R M) : (s.toSubmodule : Set M) = s := rfl

@[simp]
lemma mem_toSubmodule_iff (x : M) (s : ClosedSubmodule R M) : x âˆˆ s.toSubmodule â†” x âˆˆ s := by
  rfl

@[simp]
lemma coe_toCloseds (s : ClosedSubmodule R M) : (s.toCloseds : Set M) = s := rfl

lemma isClosed (s : ClosedSubmodule R M) : IsClosed (s : Set M) := s.isClosed'

initialize_simps_projections ClosedSubmodule (carrier â†’ coe, as_prefix coe)

instance : CanLift (Submodule R M) (ClosedSubmodule R M) toSubmodule (IsClosed (X := M) Â·) where
  prf s hs := âŸ¨âŸ¨s, hsâŸ©, rflâŸ©

@[simp, norm_cast] lemma toSubmodule_le_toSubmodule {s t : ClosedSubmodule R M} :
    s.toSubmodule â‰¤ t.toSubmodule â†” s â‰¤ t := .rfl

/-- The preimage of a closed submodule under a continuous linear map as a closed submodule. -/
@[simps!]
def comap (f : M â†’L[R] N) (s : ClosedSubmodule R N) : ClosedSubmodule R M where
  toSubmodule := .comap (f : M â†’â‚—[R] N) s
  isClosed' := by simpa using s.isClosed.preimage f.continuous

@[simp]
lemma mem_comap {f : M â†’L[R] N} {s : ClosedSubmodule R N} {x : M} : x âˆˆ s.comap f â†” f x âˆˆ s := .rfl

@[simp] lemma toSubmodule_comap (f : M â†’L[R] N) (s : ClosedSubmodule R N) :
    (s.comap f).toSubmodule = s.toSubmodule.comap (f : M â†’â‚—[R] N) := rfl

@[simp] lemma comap_id (s : ClosedSubmodule R M) : s.comap (.id _ _) = s := rfl

lemma comap_comap (g : N â†’L[R] O) (f : M â†’L[R] N) (s : ClosedSubmodule R O) :
    (s.comap g).comap f = s.comap (g.comp f) := rfl

instance instInf : Min (ClosedSubmodule R M) where
  min s t := âŸ¨s âŠ“ t, s.isClosed.inter t.isClosedâŸ©

instance instInfSet : InfSet (ClosedSubmodule R M) where
  sInf S := âŸ¨â¨… s âˆˆ S, s, by simpa using isClosed_biInter fun x hx â†¦ x.isClosedâŸ©

@[simp, norm_cast]
lemma toSubmodule_sInf (S : Set (ClosedSubmodule R M)) :
    toSubmodule (sInf S) = â¨… s âˆˆ S, s.toSubmodule := rfl

@[simp, norm_cast]
lemma toSubmodule_iInf (f : Î¹ â†’ ClosedSubmodule R M) :
    toSubmodule (â¨… i, f i) = â¨… i, (f i).toSubmodule := by rw [iInf, toSubmodule_sInf, iInf_range]

@[simp, norm_cast]
lemma coe_sInf (S : Set (ClosedSubmodule R M)) : â†‘(sInf S) = â¨… s âˆˆ S, (s : Set M) := by
  simp [â† coe_toSubmodule]

@[simp, norm_cast]
lemma coe_iInf (f : Î¹ â†’ ClosedSubmodule R M) : â†‘(â¨… i, f i) = â¨… i, (f i : Set M) := by
  simp [â† coe_toSubmodule]

@[simp] lemma mem_sInf {S : Set (ClosedSubmodule R M)} : x âˆˆ sInf S â†” âˆ€ s âˆˆ S, x âˆˆ s := by
  simp [â† SetLike.mem_coe]

@[simp] lemma mem_iInf {f : Î¹ â†’ ClosedSubmodule R M} : x âˆˆ â¨… i, f i â†” âˆ€ i, x âˆˆ f i := by
  simp [â† SetLike.mem_coe]

instance instSemilatticeInf : SemilatticeInf (ClosedSubmodule R M) :=
  toSubmodule_injective.semilatticeInf _ fun _ _ â†¦ rfl

@[simp, norm_cast]
lemma toSubmodule_inf (s t : ClosedSubmodule R M) :
    toSubmodule (s âŠ“ t) = s.toSubmodule âŠ“ t.toSubmodule := rfl

@[simp, norm_cast] lemma coe_inf (s t : ClosedSubmodule R M) : â†‘(s âŠ“ t) = (s âŠ“ t : Set M) := rfl

@[simp] lemma mem_inf : x âˆˆ s âŠ“ t â†” x âˆˆ s âˆ§ x âˆˆ t := .rfl

instance : CompleteSemilatticeInf (ClosedSubmodule R M) where
  sInf_le s a ha _ := by
    simp only [toSubmodule_sInf, Submodule.mem_iInf]
    exact fun h â†¦ h a ha
  le_sInf s a ha b := by
    simp only [toSubmodule_sInf, Submodule.mem_iInf]
    exact fun a i hi â†¦ ha i hi a

instance : OrderTop (ClosedSubmodule R M) where
  top := âŸ¨âŠ¤, isClosed_univâŸ©
  le_top s := le_top (a := s.toSubmodule)

@[simp, norm_cast] lemma toSubmodule_top : toSubmodule (âŠ¤ : ClosedSubmodule R M) = âŠ¤ := rfl

@[simp, norm_cast] lemma coe_top : ((âŠ¤ : ClosedSubmodule R M) : Set M) = .univ := rfl

@[simp] lemma mem_top : x âˆˆ (âŠ¤ : ClosedSubmodule R M) := trivial

section T1Space
variable [T1Space M]

instance instOrderBot : OrderBot (ClosedSubmodule R M) where
  bot := âŸ¨âŠ¥, isClosed_singletonâŸ©
  bot_le s := bot_le (a := s.toSubmodule)

@[simp, norm_cast] lemma toSubmodule_bot : toSubmodule (âŠ¥ : ClosedSubmodule R M) = âŠ¥ := rfl

@[simp, norm_cast] lemma coe_bot : ((âŠ¥ : ClosedSubmodule R M) : Set M) = {0} := rfl

@[simp] lemma mem_bot : x âˆˆ (âŠ¥ : ClosedSubmodule R M) â†” x = 0 := .rfl

end T1Space
end ClosedSubmodule

namespace Submodule
variable [ContinuousAdd M] [ContinuousConstSMul R M]

/-- The closure of a submodule as a closed submodule. -/
@[simps!]
protected def closure (s : Submodule R M) : ClosedSubmodule R M where
  toSubmodule := s.topologicalClosure
  isClosed' := isClosed_closure

@[simp] lemma closure_le {s : Submodule R M} {t : ClosedSubmodule R M} : s.closure â‰¤ t â†” s â‰¤ t :=
  t.isClosed.closure_subset_iff

@[simp]
lemma mem_closure_iff {x : M} {s : Submodule R M} : x âˆˆ s.closure â†” x âˆˆ s.topologicalClosure :=
  Iff.rfl

@[simp]
lemma closure_eq {s : ClosedSubmodule R M} : s.closure = s := by
  ext
  simp only [carrier_eq_coe, ClosedSubmodule.coe_toSubmodule, coe_closure, SetLike.mem_coe]
  rw [closure_eq_iff_isClosed.mpr]
  Â· rfl
  Â· exact s.isClosed'

lemma closure_eq' {s : Submodule R M} (hs : IsClosed s.carrier) : s.closure = âŸ¨s, hsâŸ© := by
  ext; simp

end Submodule

namespace ClosedSubmodule

variable [ContinuousAdd N] [ContinuousConstSMul R N] {f : M â†’L[R] N}

lemma closure_toSubmodule_eq {s : ClosedSubmodule R N} : s.toSubmodule.closure = s := by
  ext x; simp

/-- The closure of the image of a closed submodule under a continuous linear map is a closed
submodule.

`ClosedSubmodule.map f` is left-adjoint to `ClosedSubmodule.comap f`.
See `ClosedSubmodule.gc_map_comap`. -/
def map (f : M â†’L[R] N) (s : ClosedSubmodule R M) : ClosedSubmodule R N :=
  (s.toSubmodule.map (f : M â†’â‚—[R] N)).closure

@[simp]
lemma map_id [ContinuousAdd M] [ContinuousConstSMul R M] (s : ClosedSubmodule R M) :
    s.map (.id _ _) = s := SetLike.coe_injective <| by simp [map]

lemma map_le_iff_le_comap {s : ClosedSubmodule R M} {t : ClosedSubmodule R N} :
    map f s â‰¤ t â†” s â‰¤ comap f t := by
  simp [map, Submodule.map_le_iff_le_comap]; simp [â† toSubmodule_le_toSubmodule]

lemma gc_map_comap : GaloisConnection (map f) (comap f) := fun _ _ â†¦ map_le_iff_le_comap

variable {s t : ClosedSubmodule R N} {x : N}

instance : Max (ClosedSubmodule R N) where
  max s t := (s.toSubmodule âŠ” t.toSubmodule).closure

@[simp]
lemma toSubmodule_sup :
  toSubmodule (s âŠ” t) = (s.toSubmodule âŠ” t.toSubmodule).closure := rfl

@[simp, norm_cast]
lemma coe_sup :
    â†‘(s âŠ” t) = closure (s.toSubmodule âŠ” t.toSubmodule).carrier := by
  simp only [â† coe_toSubmodule, toSubmodule_sup]
  simp only [coe_toSubmodule, Submodule.coe_closure, Submodule.carrier_eq_coe]

@[simp] lemma mem_sup :
    x âˆˆ s âŠ” t â†” x âˆˆ closure (s.toSubmodule âŠ” t.toSubmodule).carrier := Iff.rfl

instance : SupSet (ClosedSubmodule R N) where
  sSup S := âŸ¨(â¨† s âˆˆ S, s.toSubmodule).closure, isClosed_closureâŸ©

@[simp]
lemma toSubmodule_sSup (S : Set (ClosedSubmodule R N)) :
    toSubmodule (sSup S) = (â¨† s âˆˆ S, s.toSubmodule).closure := rfl

@[simp]
lemma toSubmodule_iSup (f : Î¹ â†’ ClosedSubmodule R N) :
    toSubmodule (â¨† i, f i) = (â¨† i, (f i).toSubmodule).closure := by
  rw [iSup, toSubmodule_sSup, iSup_range]

@[simp, norm_cast]
lemma coe_sSup (S : Set (ClosedSubmodule R N)) :
    â†‘(sSup S) = closure (â¨† s âˆˆ S, s.toSubmodule).carrier := by
  simp only [â† coe_toSubmodule, toSubmodule_sSup]
  simp only [coe_toSubmodule, Submodule.coe_closure, Submodule.carrier_eq_coe]

@[simp, norm_cast]
lemma coe_iSup (f : Î¹ â†’ ClosedSubmodule R N) :
    â†‘(â¨† i, f i) = closure (â¨† i, (f i).toSubmodule).carrier := by
  simp only [â† coe_toSubmodule, toSubmodule_iSup, Submodule.carrier_eq_coe]
  rfl

@[simp] lemma mem_sSup {S : Set (ClosedSubmodule R N)} :
    x âˆˆ sSup S â†” x âˆˆ closure (â¨† s âˆˆ S, s.toSubmodule).carrier := Iff.rfl

@[simp] lemma mem_iSup {f : Î¹ â†’ ClosedSubmodule R N} :
    x âˆˆ â¨† i, f i â†” x âˆˆ closure (â¨† i, (f i).toSubmodule).carrier := by
  simp [â† SetLike.mem_coe]

instance : SemilatticeSup (ClosedSubmodule R N) where
  sup s t := s âŠ” t
  le_sup_left _ _ _ hx := subset_closure <| Submodule.mem_sup_left hx
  le_sup_right _ _ _ hx := subset_closure <| Submodule.mem_sup_right hx
  sup_le _ _ _ ha hb := Submodule.closure_le.mpr <| sup_le_iff.mpr âŸ¨ha, hbâŸ©

instance : CompleteSemilatticeSup (ClosedSubmodule R N) where
  le_sSup s a ha x hx := subset_closure <| Submodule.mem_iSup_of_mem _ <|
    Submodule.mem_iSup_of_mem ha hx
  sSup_le s a h x := by
    rw [â† ClosedSubmodule.closure_toSubmodule_eq (s := a)]
    apply closure_mono
    simp only [Submodule.coe_toAddSubmonoid, coe_toSubmodule]
    intro y hy
    simp only [SetLike.mem_coe, Submodule.mem_iSup] at hy
    exact hy a fun b _ hz â†¦ Submodule.mem_iSup _ |>.mp hz _ <| fun hb â†¦ h b hb

instance : Lattice (ClosedSubmodule R N) where

instance [T1Space N] : CompleteLattice (ClosedSubmodule R N) where

end ClosedSubmodule

section CompleteSpace

instance {ð•œ H : Type*} [Semiring ð•œ] [AddCommMonoid H] [UniformSpace H] [Module ð•œ H]
    [CompleteSpace H] (K : ClosedSubmodule ð•œ H) : CompleteSpace K := by
  apply IsComplete.completeSpace_coe
  rw [â† ClosedSubmodule.carrier_eq_coe]
  exact K.isClosed'.isComplete

end CompleteSpace
