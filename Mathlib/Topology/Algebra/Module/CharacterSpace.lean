/-
Copyright (c) 2022 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis
-/
import Mathlib.Topology.Algebra.Module.WeakDual
import Mathlib.Algebra.Algebra.Spectrum
import Mathlib.Topology.ContinuousFunction.Algebra
import Mathlib.Data.Set.Lattice

#align_import topology.algebra.module.character_space from "leanprover-community/mathlib"@"a148d797a1094ab554ad4183a4ad6f130358ef64"

/-!
# Character space of a topological algebra

The character space of a topological algebra is the subset of elements of the weak dual that
are also algebra homomorphisms. This space is used in the Gelfand transform, which gives an
isomorphism between a commutative Câ‹†-algebra and continuous functions on the character space
of the algebra. This, in turn, is used to construct the continuous functional calculus on
Câ‹†-algebras.


## Implementation notes

We define `WeakDual.characterSpace ð•œ A` as a subset of the weak dual, which automatically puts the
correct topology on the space. We then define `WeakDual.CharacterSpace.toAlgHom` which provides the
algebra homomorphism corresponding to any element. We also provide `WeakDual.CharacterSpace.toCLM`
which provides the element as a continuous linear map. (Even though `WeakDual ð•œ A` is a type copy of
`A â†’L[ð•œ] ð•œ`, this is often more convenient.)

## Tags

character space, Gelfand transform, functional calculus

-/


namespace WeakDual

/-- The character space of a topological algebra is the subset of elements of the weak dual that
are also algebra homomorphisms. -/
def characterSpace (ð•œ : Type*) (A : Type*) [CommSemiring ð•œ] [TopologicalSpace ð•œ] [ContinuousAdd ð•œ]
    [ContinuousConstSMul ð•œ ð•œ] [NonUnitalNonAssocSemiring A] [TopologicalSpace A] [Module ð•œ A] :=
  {Ï† : WeakDual ð•œ A | Ï† â‰  0 âˆ§ âˆ€ x y : A, Ï† (x * y) = Ï† x * Ï† y}
#align weak_dual.character_space WeakDual.characterSpace

variable {ð•œ : Type*} {A : Type*}

-- Porting note: even though the capitalization of the namespace differs, it doesn't matter
-- because there is no dot notation since `characterSpace` is only a type via `CoeSort`.
namespace CharacterSpace

section NonUnitalNonAssocSemiring

variable [CommSemiring ð•œ] [TopologicalSpace ð•œ] [ContinuousAdd ð•œ] [ContinuousConstSMul ð•œ ð•œ]
  [NonUnitalNonAssocSemiring A] [TopologicalSpace A] [Module ð•œ A]

instance instFunLike : FunLike (characterSpace ð•œ A) A ð•œ where
  coe Ï† := ((Ï† : WeakDual ð•œ A) : A â†’ ð•œ)
  coe_injective' Ï† Ïˆ h := by ext1; apply DFunLike.ext; exact congr_fun h

/-- Elements of the character space are continuous linear maps. -/
instance instContinuousLinearMapClass : ContinuousLinearMapClass (characterSpace ð•œ A) ð•œ A ð•œ where
  map_smulâ‚›â‚— Ï† := (Ï† : WeakDual ð•œ A).map_smul
  map_add Ï† := (Ï† : WeakDual ð•œ A).map_add
  map_continuous Ï† := (Ï† : WeakDual ð•œ A).cont

-- Porting note: moved because Lean 4 doesn't see the `DFunLike` instance on `characterSpace ð•œ A`
-- until the `ContinuousLinearMapClass` instance is declared
@[simp, norm_cast]
protected theorem coe_coe (Ï† : characterSpace ð•œ A) : â‡‘(Ï† : WeakDual ð•œ A) = (Ï† : A â†’ ð•œ) :=
  rfl
#align weak_dual.character_space.coe_coe WeakDual.CharacterSpace.coe_coe

@[ext]
theorem ext {Ï† Ïˆ : characterSpace ð•œ A} (h : âˆ€ x, Ï† x = Ïˆ x) : Ï† = Ïˆ :=
  DFunLike.ext _ _ h
#align weak_dual.character_space.ext WeakDual.CharacterSpace.ext

/-- An element of the character space, as a continuous linear map. -/
def toCLM (Ï† : characterSpace ð•œ A) : A â†’L[ð•œ] ð•œ :=
  (Ï† : WeakDual ð•œ A)
#align weak_dual.character_space.to_clm WeakDual.CharacterSpace.toCLM

@[simp]
theorem coe_toCLM (Ï† : characterSpace ð•œ A) : â‡‘(toCLM Ï†) = Ï† :=
  rfl
#align weak_dual.character_space.coe_to_clm WeakDual.CharacterSpace.coe_toCLM

/-- Elements of the character space are non-unital algebra homomorphisms. -/
instance instNonUnitalAlgHomClass : NonUnitalAlgHomClass (characterSpace ð•œ A) ð•œ A ð•œ :=
  { CharacterSpace.instContinuousLinearMapClass with
    map_smulâ‚›â‚— := fun Ï† => map_smul Ï†
    map_zero := fun Ï† => map_zero Ï†
    map_mul := fun Ï† => Ï†.prop.2 }

/-- An element of the character space, as a non-unital algebra homomorphism. -/
def toNonUnitalAlgHom (Ï† : characterSpace ð•œ A) : A â†’â‚™â‚[ð•œ] ð•œ where
  toFun := (Ï† : A â†’ ð•œ)
  map_mul' := map_mul Ï†
  map_smul' := map_smul Ï†
  map_zero' := map_zero Ï†
  map_add' := map_add Ï†
#align weak_dual.character_space.to_non_unital_alg_hom WeakDual.CharacterSpace.toNonUnitalAlgHom

@[simp]
theorem coe_toNonUnitalAlgHom (Ï† : characterSpace ð•œ A) : â‡‘(toNonUnitalAlgHom Ï†) = Ï† :=
  rfl
#align weak_dual.character_space.coe_to_non_unital_alg_hom WeakDual.CharacterSpace.coe_toNonUnitalAlgHom

instance instIsEmpty [Subsingleton A] : IsEmpty (characterSpace ð•œ A) :=
  âŸ¨fun Ï† => Ï†.prop.1 <|
    ContinuousLinearMap.ext fun x => by
      rw [show x = 0 from Subsingleton.elim x 0, map_zero, map_zero] âŸ©

variable (ð•œ A)

theorem union_zero :
    characterSpace ð•œ A âˆª {0} = {Ï† : WeakDual ð•œ A | âˆ€ x y : A, Ï† (x * y) = Ï† x * Ï† y} :=
  le_antisymm (by
      rintro Ï† (hÏ† | rfl)
      Â· exact hÏ†.2
      Â· exact fun _ _ => by exact (zero_mul (0 : ð•œ)).symm)
    fun Ï† hÏ† => Or.elim (em <| Ï† = 0) Or.inr fun hâ‚€ => Or.inl âŸ¨hâ‚€, hÏ†âŸ©
#align weak_dual.character_space.union_zero WeakDual.CharacterSpace.union_zero

/-- The `characterSpace ð•œ A` along with `0` is always a closed set in `WeakDual ð•œ A`. -/
theorem union_zero_isClosed [T2Space ð•œ] [ContinuousMul ð•œ] :
    IsClosed (characterSpace ð•œ A âˆª {0}) := by
  simp only [union_zero, Set.setOf_forall]
  exact
    isClosed_iInter fun x =>
      isClosed_iInter fun y =>
        isClosed_eq (eval_continuous _) <| (eval_continuous _).mul (eval_continuous _)
#align weak_dual.character_space.union_zero_is_closed WeakDual.CharacterSpace.union_zero_isClosed

end NonUnitalNonAssocSemiring

section Unital

variable [CommRing ð•œ] [NoZeroDivisors ð•œ] [TopologicalSpace ð•œ] [ContinuousAdd ð•œ]
  [ContinuousConstSMul ð•œ ð•œ] [TopologicalSpace A] [Semiring A] [Algebra ð•œ A]

/-- In a unital algebra, elements of the character space are algebra homomorphisms. -/
instance instAlgHomClass : AlgHomClass (characterSpace ð•œ A) ð•œ A ð•œ :=
  haveI map_one' : âˆ€ Ï† : characterSpace ð•œ A, Ï† 1 = 1 := fun Ï† => by
    have hâ‚ : Ï† 1 * (1 - Ï† 1) = 0 := by rw [mul_sub, sub_eq_zero, mul_one, â† map_mul Ï†, one_mul]
    rcases mul_eq_zero.mp hâ‚ with (hâ‚‚ | hâ‚‚)
    Â· have : âˆ€ a, Ï† (a * 1) = 0 := fun a => by simp only [map_mul Ï†, hâ‚‚, mul_zero]
      exact False.elim (Ï†.prop.1 <| ContinuousLinearMap.ext <| by simpa only [mul_one] using this)
    Â· exact (sub_eq_zero.mp hâ‚‚).symm
  { CharacterSpace.instNonUnitalAlgHomClass with
    map_one := map_one'
    commutes := fun Ï† r => by
      rw [Algebra.algebraMap_eq_smul_one, Algebra.id.map_eq_id, RingHom.id_apply]
      rw [map_smul, Algebra.id.smul_eq_mul, map_one' Ï†, mul_one] }

/-- An element of the character space of a unital algebra, as an algebra homomorphism. -/
@[simps]
def toAlgHom (Ï† : characterSpace ð•œ A) : A â†’â‚[ð•œ] ð•œ :=
  { toNonUnitalAlgHom Ï† with
    map_one' := map_one Ï†
    commutes' := AlgHomClass.commutes Ï† }
#align weak_dual.character_space.to_alg_hom WeakDual.CharacterSpace.toAlgHom

theorem eq_set_map_one_map_mul [Nontrivial ð•œ] :
    characterSpace ð•œ A = {Ï† : WeakDual ð•œ A | Ï† 1 = 1 âˆ§ âˆ€ x y : A, Ï† (x * y) = Ï† x * Ï† y} := by
  ext Ï†
  refine âŸ¨?_, ?_âŸ©
  Â· rintro hÏ†
    lift Ï† to characterSpace ð•œ A using hÏ†
    exact âŸ¨map_one Ï†, map_mul Ï†âŸ©
  Â· rintro âŸ¨hÏ†â‚, hÏ†â‚‚âŸ©
    refine âŸ¨?_, hÏ†â‚‚âŸ©
    rintro rfl
    exact zero_ne_one hÏ†â‚
#align weak_dual.character_space.eq_set_map_one_map_mul WeakDual.CharacterSpace.eq_set_map_one_map_mul

/-- under suitable mild assumptions on `ð•œ`, the character space is a closed set in
`WeakDual ð•œ A`. -/
protected theorem isClosed [Nontrivial ð•œ] [T2Space ð•œ] [ContinuousMul ð•œ] :
    IsClosed (characterSpace ð•œ A) := by
  rw [eq_set_map_one_map_mul, Set.setOf_and]
  refine IsClosed.inter (isClosed_eq (eval_continuous _) continuous_const) ?_
  simpa only [(union_zero ð•œ A).symm] using union_zero_isClosed _ _
#align weak_dual.character_space.is_closed WeakDual.CharacterSpace.isClosed

end Unital

section Ring

variable [CommRing ð•œ] [NoZeroDivisors ð•œ] [TopologicalSpace ð•œ] [ContinuousAdd ð•œ]
  [ContinuousConstSMul ð•œ ð•œ] [TopologicalSpace A] [Ring A] [Algebra ð•œ A]

theorem apply_mem_spectrum [Nontrivial ð•œ] (Ï† : characterSpace ð•œ A) (a : A) : Ï† a âˆˆ spectrum ð•œ a :=
  AlgHom.apply_mem_spectrum Ï† a
#align weak_dual.character_space.apply_mem_spectrum WeakDual.CharacterSpace.apply_mem_spectrum

theorem ext_ker {Ï† Ïˆ : characterSpace ð•œ A} (h : RingHom.ker Ï† = RingHom.ker Ïˆ) : Ï† = Ïˆ := by
  ext x
  have : x - algebraMap ð•œ A (Ïˆ x) âˆˆ RingHom.ker Ï† := by
    simpa only [h, RingHom.mem_ker, map_sub, AlgHomClass.commutes] using sub_self (Ïˆ x)
  rwa [RingHom.mem_ker, map_sub, AlgHomClass.commutes, sub_eq_zero] at this
#align weak_dual.character_space.ext_ker WeakDual.CharacterSpace.ext_ker

end Ring

end CharacterSpace

section Kernel

variable [Field ð•œ] [TopologicalSpace ð•œ] [ContinuousAdd ð•œ] [ContinuousConstSMul ð•œ ð•œ]
variable [Ring A] [TopologicalSpace A] [Algebra ð•œ A]

/-- The `RingHom.ker` of `Ï† : characterSpace ð•œ A` is maximal. -/
instance ker_isMaximal (Ï† : characterSpace ð•œ A) : (RingHom.ker Ï†).IsMaximal :=
  RingHom.ker_isMaximal_of_surjective Ï† fun z =>
    âŸ¨algebraMap ð•œ A z, by simp only [AlgHomClass.commutes, Algebra.id.map_eq_id, RingHom.id_apply]âŸ©
#align weak_dual.ker_is_maximal WeakDual.ker_isMaximal

end Kernel

section GelfandTransform

open ContinuousMap

variable (ð•œ A) [CommRing ð•œ] [NoZeroDivisors ð•œ] [TopologicalSpace ð•œ] [TopologicalRing ð•œ]
  [TopologicalSpace A] [Semiring A] [Algebra ð•œ A]

/-- The **Gelfand transform** is an algebra homomorphism (over `ð•œ`) from a topological `ð•œ`-algebra
`A` into the `ð•œ`-algebra of continuous `ð•œ`-valued functions on the `characterSpace ð•œ A`.
The character space itself consists of all algebra homomorphisms from `A` to `ð•œ`.  -/
@[simps]
def gelfandTransform : A â†’â‚[ð•œ] C(characterSpace ð•œ A, ð•œ) where
  toFun a :=
    { toFun := fun Ï† => Ï† a
      continuous_toFun := (eval_continuous a).comp continuous_induced_dom }
  map_one' := by ext a; simp only [coe_mk, coe_one, Pi.one_apply, map_one a]
  map_mul' a b := by ext; simp only [map_mul, coe_mk, coe_mul, Pi.mul_apply]
  map_zero' := by ext; simp only [map_zero, coe_mk, coe_mul, coe_zero, Pi.zero_apply]
  map_add' a b := by ext; simp only [map_add, coe_mk, coe_add, Pi.add_apply]
  commutes' k := by ext; simp [AlgHomClass.commutes]
#align weak_dual.gelfand_transform WeakDual.gelfandTransform

end GelfandTransform

end WeakDual
