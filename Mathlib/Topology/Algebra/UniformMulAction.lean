/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Algebra.Module.Opposite
import Mathlib.Topology.UniformSpace.Completion
import Mathlib.Topology.Algebra.IsUniformGroup.Defs

/-!
# Multiplicative action on the completion of a uniform space

In this file we define typeclasses `UniformContinuousConstVAdd` and
`UniformContinuousConstSMul` and prove that a multiplicative action on `X` with uniformly
continuous `(‚Ä¢) c` can be extended to a multiplicative action on `UniformSpace.Completion X`.

In later files once the additive group structure is set up, we provide
* `UniformSpace.Completion.DistribMulAction`
* `UniformSpace.Completion.MulActionWithZero`
* `UniformSpace.Completion.Module`

TODO: Generalise the results here from the concrete `Completion` to any `AbstractCompletion`.
-/


universe u v w x y

open scoped Uniformity

noncomputable section

variable (R : Type u) (M : Type v) (N : Type w) (X : Type x) (Y : Type y) [UniformSpace X]
  [UniformSpace Y]

/-- An additive action such that for all `c`, the map `fun x ‚Ü¶ c +·µ• x` is uniformly continuous. -/
class UniformContinuousConstVAdd [VAdd M X] : Prop where
  uniformContinuous_const_vadd : ‚àÄ c : M, UniformContinuous (c +·µ• ¬∑ : X ‚Üí X)

/-- A multiplicative action such that for all `c`,
the map `fun x ‚Ü¶ c ‚Ä¢ x` is uniformly continuous. -/
@[to_additive]
class UniformContinuousConstSMul [SMul M X] : Prop where
  uniformContinuous_const_smul : ‚àÄ c : M, UniformContinuous (c ‚Ä¢ ¬∑ : X ‚Üí X)

export UniformContinuousConstVAdd (uniformContinuous_const_vadd)

export UniformContinuousConstSMul (uniformContinuous_const_smul)

instance AddMonoid.uniformContinuousConstSMul_nat [AddGroup X] [IsUniformAddGroup X] :
    UniformContinuousConstSMul ‚Ñï X :=
  ‚ü®uniformContinuous_const_nsmul‚ü©

instance AddGroup.uniformContinuousConstSMul_int [AddGroup X] [IsUniformAddGroup X] :
    UniformContinuousConstSMul ‚Ñ§ X :=
  ‚ü®uniformContinuous_const_zsmul‚ü©

/-- A `DistribMulAction` that is continuous on a uniform group is uniformly continuous.
This can't be an instance due to it forming a loop with
`UniformContinuousConstSMul.to_continuousConstSMul` -/
theorem uniformContinuousConstSMul_of_continuousConstSMul [Monoid R] [AddGroup M]
    [DistribMulAction R M] [UniformSpace M] [IsUniformAddGroup M] [ContinuousConstSMul R M] :
    UniformContinuousConstSMul R M :=
  ‚ü®fun r ‚Ü¶
    uniformContinuous_of_continuousAt_zero (DistribMulAction.toAddMonoidHom M r)
      (Continuous.continuousAt (continuous_const_smul r))‚ü©

/-- The action of `Semiring.toModule` is uniformly continuous. -/
instance Ring.uniformContinuousConstSMul [Ring R] [UniformSpace R] [IsUniformAddGroup R]
    [ContinuousMul R] : UniformContinuousConstSMul R R :=
  uniformContinuousConstSMul_of_continuousConstSMul _ _

/-- The action of `Semiring.toOppositeModule` is uniformly continuous. -/
instance Ring.uniformContinuousConstSMul_op [Ring R] [UniformSpace R] [IsUniformAddGroup R]
    [ContinuousMul R] : UniformContinuousConstSMul R·µê·µí·µñ R :=
  uniformContinuousConstSMul_of_continuousConstSMul _ _

section SMul

variable [SMul M X]

@[to_additive]
instance (priority := 100) UniformContinuousConstSMul.to_continuousConstSMul
    [UniformContinuousConstSMul M X] : ContinuousConstSMul M X :=
  ‚ü®fun c ‚Ü¶ (uniformContinuous_const_smul c).continuous‚ü©

variable {M X Y}

@[to_additive]
theorem UniformContinuous.const_smul [UniformContinuousConstSMul M X] {f : Y ‚Üí X}
    (hf : UniformContinuous f) (c : M) : UniformContinuous (c ‚Ä¢ f) :=
  (uniformContinuous_const_smul c).comp hf

@[to_additive]
lemma IsUniformInducing.uniformContinuousConstSMul [SMul M Y] [UniformContinuousConstSMul M Y]
    {f : X ‚Üí Y} (hf : IsUniformInducing f) (hsmul : ‚àÄ (c : M) x, f (c ‚Ä¢ x) = c ‚Ä¢ f x) :
    UniformContinuousConstSMul M X where
  uniformContinuous_const_smul c := by
    simpa only [hf.uniformContinuous_iff, Function.comp_def, hsmul]
      using hf.uniformContinuous.const_smul c

/-- If a scalar action is central, then its right action is uniform continuous when its left action
is. -/
@[to_additive /-- If an additive action is central, then its right action is uniform
continuous when its left action is. -/]
instance (priority := 100) UniformContinuousConstSMul.op [SMul M·µê·µí·µñ X] [IsCentralScalar M X]
    [UniformContinuousConstSMul M X] : UniformContinuousConstSMul M·µê·µí·µñ X :=
  ‚ü®MulOpposite.rec' fun c ‚Ü¶ by simpa only [op_smul_eq_smul] using uniformContinuous_const_smul c‚ü©

@[to_additive]
instance MulOpposite.uniformContinuousConstSMul [UniformContinuousConstSMul M X] :
    UniformContinuousConstSMul M X·µê·µí·µñ :=
  ‚ü®fun c ‚Ü¶
    MulOpposite.uniformContinuous_op.comp <| MulOpposite.uniformContinuous_unop.const_smul c‚ü©

end SMul

@[to_additive]
instance IsUniformGroup.to_uniformContinuousConstSMul {G : Type u} [Group G] [UniformSpace G]
    [IsUniformGroup G] : UniformContinuousConstSMul G G :=
  ‚ü®fun _ ‚Ü¶ uniformContinuous_const.mul uniformContinuous_id‚ü©

section Ring

variable {R Œ≤ : Type*} [Ring R] [UniformSpace R] [UniformSpace Œ≤]

theorem UniformContinuous.const_mul' [UniformContinuousConstSMul R R] {f : Œ≤ ‚Üí R}
    (hf : UniformContinuous f) (a : R) : UniformContinuous fun x ‚Ü¶ a * f x :=
  hf.const_smul a

theorem UniformContinuous.mul_const' [UniformContinuousConstSMul R·µê·µí·µñ R] {f : Œ≤ ‚Üí R}
    (hf : UniformContinuous f) (a : R) : UniformContinuous fun x ‚Ü¶ f x * a :=
  hf.const_smul (MulOpposite.op a)

theorem uniformContinuous_mul_left' [UniformContinuousConstSMul R R] (a : R) :
    UniformContinuous fun b : R ‚Ü¶ a * b :=
  uniformContinuous_id.const_mul' _

theorem uniformContinuous_mul_right' [UniformContinuousConstSMul R·µê·µí·µñ R] (a : R) :
    UniformContinuous fun b : R ‚Ü¶ b * a :=
  uniformContinuous_id.mul_const' _

theorem UniformContinuous.div_const' {R Œ≤ : Type*} [DivisionRing R] [UniformSpace R]
    [UniformContinuousConstSMul R·µê·µí·µñ R] [UniformSpace Œ≤] {f : Œ≤ ‚Üí R}
    (hf : UniformContinuous f) (a : R) :
    UniformContinuous fun x ‚Ü¶ f x / a := by
  simpa [div_eq_mul_inv] using hf.mul_const' a‚Åª¬π

theorem uniformContinuous_div_const' {R : Type*} [DivisionRing R] [UniformSpace R]
    [UniformContinuousConstSMul R·µê·µí·µñ R] (a : R) :
    UniformContinuous fun b : R ‚Ü¶ b / a :=
  uniformContinuous_id.div_const' _

end Ring

section Unit

open scoped Pointwise

variable {M X}

@[to_additive]
theorem IsUnit.smul_uniformity [Monoid M] [MulAction M X] [UniformContinuousConstSMul M X] {c : M}
    (hc : IsUnit c) : c ‚Ä¢ ùì§ X = ùì§ X :=
  let ‚ü®d, hcd‚ü© := hc.exists_right_inv
  have cU : c ‚Ä¢ ùì§ X ‚â§ ùì§ X := uniformContinuous_const_smul c
  have dU : d ‚Ä¢ ùì§ X ‚â§ ùì§ X := uniformContinuous_const_smul d
  le_antisymm cU <| by simpa [smul_smul, hcd] using Filter.smul_filter_le_smul_filter (a := c) dU

@[to_additive (attr := simp)]
theorem smul_uniformity [Group M] [MulAction M X] [UniformContinuousConstSMul M X] (c : M) :
    c ‚Ä¢ ùì§ X = ùì§ X :=
  Group.isUnit _ |>.smul_uniformity

theorem smul_uniformity‚ÇÄ [GroupWithZero M] [MulAction M X] [UniformContinuousConstSMul M X] {c : M}
    (hc : c ‚â† 0) : c ‚Ä¢ ùì§ X = ùì§ X :=
  hc.isUnit.smul_uniformity

end Unit

namespace UniformSpace

namespace Completion

section SMul

variable [SMul M X]

@[to_additive]
noncomputable instance : SMul M (Completion X) :=
  ‚ü®fun c ‚Ü¶ Completion.map (c ‚Ä¢ ¬∑)‚ü©

@[to_additive]
theorem smul_def (c : M) (x : Completion X) : c ‚Ä¢ x = Completion.map (c ‚Ä¢ ¬∑) x :=
  rfl

@[to_additive]
instance : UniformContinuousConstSMul M (Completion X) :=
  ‚ü®fun _ ‚Ü¶ uniformContinuous_map‚ü©

@[to_additive]
instance instIsScalarTower [SMul N X] [SMul M N] [UniformContinuousConstSMul M X]
    [UniformContinuousConstSMul N X] [IsScalarTower M N X] : IsScalarTower M N (Completion X) :=
  ‚ü®fun m n x ‚Ü¶ by
    have : _ = (_ : Completion X ‚Üí Completion X) :=
      map_comp (uniformContinuous_const_smul m) (uniformContinuous_const_smul n)
    refine Eq.trans ?_ (congr_fun this.symm x)
    exact congr_arg (fun f ‚Ü¶ Completion.map f x) (funext (smul_assoc _ _))‚ü©

@[to_additive]
instance [SMul N X] [SMulCommClass M N X] [UniformContinuousConstSMul M X]
    [UniformContinuousConstSMul N X] : SMulCommClass M N (Completion X) :=
  ‚ü®fun m n x ‚Ü¶ by
    have hmn : m ‚Ä¢ n ‚Ä¢ x = (Completion.map (SMul.smul m) ‚àò Completion.map (SMul.smul n)) x := rfl
    have hnm : n ‚Ä¢ m ‚Ä¢ x = (Completion.map (SMul.smul n) ‚àò Completion.map (SMul.smul m)) x := rfl
    rw [hmn, hnm, map_comp, map_comp]
    ¬∑ exact congr_arg (fun f ‚Ü¶ Completion.map f x) (funext (smul_comm _ _))
    repeat' exact uniformContinuous_const_smul _‚ü©

@[to_additive]
instance [SMul M·µê·µí·µñ X] [IsCentralScalar M X] : IsCentralScalar M (Completion X) :=
  ‚ü®fun c a ‚Ü¶ (congr_arg fun f ‚Ü¶ Completion.map f a) <| funext (op_smul_eq_smul c)‚ü©

variable {M X}
variable [UniformContinuousConstSMul M X]

@[to_additive (attr := simp, norm_cast)]
theorem coe_smul (c : M) (x : X) : (‚Üë(c ‚Ä¢ x) : Completion X) = c ‚Ä¢ (x : Completion X) :=
  (map_coe (uniformContinuous_const_smul c) x).symm

end SMul

@[to_additive]
noncomputable instance [Monoid M] [MulAction M X] [UniformContinuousConstSMul M X] :
    MulAction M (Completion X) where
  smul := (¬∑ ‚Ä¢ ¬∑)
  one_smul := ext' (continuous_const_smul _) continuous_id fun a ‚Ü¶ by rw [‚Üê coe_smul, one_smul]
  mul_smul x y :=
    ext' (continuous_const_smul _) ((continuous_const_smul _).const_smul _) fun a ‚Ü¶ by
      simp only [‚Üê coe_smul, mul_smul]

end Completion

end UniformSpace
