/-
Copyright (c) 2022 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import Mathlib.Topology.Algebra.UniformGroup
import Mathlib.Topology.UniformSpace.Completion

#align_import topology.algebra.uniform_mul_action from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!
# Multiplicative action on the completion of a uniform space

In this file we define typeclasses `UniformContinuousConstVAdd` and
`UniformContinuousConstSMul` and prove that a multiplicative action on `X` with uniformly
continuous `(â€¢) c` can be extended to a multiplicative action on `UniformSpace.Completion X`.

In later files once the additive group structure is set up, we provide
* `UniformSpace.Completion.DistribMulAction`
* `UniformSpace.Completion.MulActionWithZero`
* `UniformSpace.Completion.Module`

TODO: Generalise the results here from the concrete `Completion` to any `AbstractCompletion`.
-/


universe u v w x y

noncomputable section

variable (R : Type u) (M : Type v) (N : Type w) (X : Type x) (Y : Type y) [UniformSpace X]
  [UniformSpace Y]

/-- An additive action such that for all `c`, the map `fun x â†¦ c +áµ¥ x` is uniformly continuous. -/
class UniformContinuousConstVAdd [VAdd M X] : Prop where
  uniformContinuous_const_vadd : âˆ€ c : M, UniformContinuous ((Â· +áµ¥ Â·) c : X â†’ X)
#align has_uniform_continuous_const_vadd UniformContinuousConstVAdd

/-- A multiplicative action such that for all `c`, the map `Î» x, c â€¢ x` is uniformly continuous. -/
@[to_additive]
class UniformContinuousConstSMul [SMul M X] : Prop where
  uniformContinuous_const_smul : âˆ€ c : M, UniformContinuous ((Â· â€¢ Â·) c : X â†’ X)
#align has_uniform_continuous_const_smul UniformContinuousConstSMul

export UniformContinuousConstVAdd (uniformContinuous_const_vadd)

export UniformContinuousConstSMul (uniformContinuous_const_smul)

instance AddMonoid.uniformContinuousConstSMul_nat [AddGroup X] [UniformAddGroup X] :
    UniformContinuousConstSMul â„• X :=
  âŸ¨uniformContinuous_const_nsmulâŸ©
#align add_monoid.has_uniform_continuous_const_smul_nat AddMonoid.uniformContinuousConstSMul_nat

instance AddGroup.uniformContinuousConstSMul_int [AddGroup X] [UniformAddGroup X] :
    UniformContinuousConstSMul â„¤ X :=
  âŸ¨uniformContinuous_const_zsmulâŸ©
#align add_group.has_uniform_continuous_const_smul_int AddGroup.uniformContinuousConstSMul_int

/-- A `DistribMulAction` that is continuous on a uniform group is uniformly continuous.
This can't be an instance due to it forming a loop with
`UniformContinuousConstSMul.to_continuousConstSMul` -/
theorem uniformContinuousConstSMul_of_continuousConstSMul [Monoid R] [AddCommGroup M]
    [DistribMulAction R M] [UniformSpace M] [UniformAddGroup M] [ContinuousConstSMul R M] :
    UniformContinuousConstSMul R M :=
  âŸ¨fun r =>
    uniformContinuous_of_continuousAt_zero (DistribMulAction.toAddMonoidHom M r)
      (Continuous.continuousAt (continuous_const_smul r))âŸ©
#align has_uniform_continuous_const_smul_of_continuous_const_smul uniformContinuousConstSMul_of_continuousConstSMul

/-- The action of `Semiring.toModule` is uniformly continuous. -/
instance Ring.uniformContinuousConstSMul [Ring R] [UniformSpace R] [UniformAddGroup R]
    [ContinuousMul R] : UniformContinuousConstSMul R R :=
  uniformContinuousConstSMul_of_continuousConstSMul _ _
#align ring.has_uniform_continuous_const_smul Ring.uniformContinuousConstSMul

/-- The action of `Semiring.toOppositeModule` is uniformly continuous. -/
instance Ring.uniformContinuousConstSMul_op [Ring R] [UniformSpace R] [UniformAddGroup R]
    [ContinuousMul R] : UniformContinuousConstSMul Ráµáµ’áµ– R :=
  uniformContinuousConstSMul_of_continuousConstSMul _ _
#align ring.has_uniform_continuous_const_op_smul Ring.uniformContinuousConstSMul_op

section SMul

variable [SMul M X]

@[to_additive]
instance (priority := 100) UniformContinuousConstSMul.to_continuousConstSMul
    [UniformContinuousConstSMul M X] : ContinuousConstSMul M X :=
  âŸ¨fun c => (uniformContinuous_const_smul c).continuousâŸ©
#align has_uniform_continuous_const_smul.to_has_continuous_const_smul UniformContinuousConstSMul.to_continuousConstSMul
#align has_uniform_continuous_const_vadd.to_has_continuous_const_vadd UniformContinuousConstVAdd.to_continuousConstVAdd

variable {M X Y}

@[to_additive]
theorem UniformContinuous.const_smul [UniformContinuousConstSMul M X] {f : Y â†’ X}
    (hf : UniformContinuous f) (c : M) : UniformContinuous (c â€¢ f) :=
  (uniformContinuous_const_smul c).comp hf
#align uniform_continuous.const_smul UniformContinuous.const_smul
#align uniform_continuous.const_vadd UniformContinuous.const_vadd

/-- If a scalar action is central, then its right action is uniform continuous when its left action
is. -/
@[to_additive "If an additive action is central, then its right action is uniform
continuous when its left action is."]
instance (priority := 100) UniformContinuousConstSMul.op [SMul Máµáµ’áµ– X] [IsCentralScalar M X]
    [UniformContinuousConstSMul M X] : UniformContinuousConstSMul Máµáµ’áµ– X :=
  âŸ¨MulOpposite.rec' fun c => by
    dsimp only
    -- âŠ¢ UniformContinuous fun x => MulOpposite.op c â€¢ x
    simp_rw [op_smul_eq_smul]
    -- âŠ¢ UniformContinuous fun x => c â€¢ x
    exact uniformContinuous_const_smul câŸ©
    -- ğŸ‰ no goals
#align has_uniform_continuous_const_smul.op UniformContinuousConstSMul.op
#align has_uniform_continuous_const_vadd.op UniformContinuousConstVAdd.op

@[to_additive]
instance MulOpposite.uniformContinuousConstSMul [UniformContinuousConstSMul M X] :
    UniformContinuousConstSMul M Xáµáµ’áµ– :=
  âŸ¨fun c =>
    MulOpposite.uniformContinuous_op.comp <| MulOpposite.uniformContinuous_unop.const_smul câŸ©
#align mul_opposite.has_uniform_continuous_const_smul MulOpposite.uniformContinuousConstSMul
#align add_opposite.has_uniform_continuous_const_vadd AddOpposite.uniformContinuousConstVAdd

end SMul

@[to_additive]
instance UniformGroup.to_uniformContinuousConstSMul {G : Type u} [Group G] [UniformSpace G]
    [UniformGroup G] : UniformContinuousConstSMul G G :=
  âŸ¨fun _ => uniformContinuous_const.mul uniformContinuous_idâŸ©
#align uniform_group.to_has_uniform_continuous_const_smul UniformGroup.to_uniformContinuousConstSMul
#align uniform_add_group.to_has_uniform_continuous_const_vadd UniformAddGroup.to_uniformContinuousConstVAdd

namespace UniformSpace

namespace Completion

section SMul

variable [SMul M X]

@[to_additive]
noncomputable instance : SMul M (Completion X) :=
  âŸ¨fun c => Completion.map ((Â· â€¢ Â·) c)âŸ©

@[to_additive]
theorem smul_def (c : M) (x : Completion X) : c â€¢ x = Completion.map (c â€¢ Â·) x :=
  rfl
#align uniform_space.completion.smul_def UniformSpace.Completion.smul_def
#align uniform_space.completion.vadd_def UniformSpace.Completion.vadd_def

@[to_additive]
instance : UniformContinuousConstSMul M (Completion X) :=
  âŸ¨fun _ => uniformContinuous_mapâŸ©

@[to_additive instVAddAssocClass]
instance instIsScalarTower [SMul N X] [SMul M N] [UniformContinuousConstSMul M X]
    [UniformContinuousConstSMul N X] [IsScalarTower M N X] : IsScalarTower M N (Completion X) :=
  âŸ¨fun m n x => by
    have : _ = (_ : Completion X â†’ Completion X) :=
      map_comp (uniformContinuous_const_smul m) (uniformContinuous_const_smul n)
    refine' Eq.trans _ (congr_fun this.symm x)
    -- âŠ¢ (m â€¢ n) â€¢ x = Completion.map ((fun x x_1 => x â€¢ x_1) m âˆ˜ (fun x x_1 => x â€¢ x â€¦
    exact congr_arg (fun f => Completion.map f x) (funext (smul_assoc _ _))âŸ©
    -- ğŸ‰ no goals
#align uniform_space.completion.is_scalar_tower UniformSpace.Completion.instIsScalarTower
#align uniform_space.completion.vadd_assoc_class UniformSpace.Completion.instVAddAssocClass

@[to_additive]
instance [SMul N X] [SMulCommClass M N X] [UniformContinuousConstSMul M X]
    [UniformContinuousConstSMul N X] : SMulCommClass M N (Completion X) :=
  âŸ¨fun m n x => by
    have hmn : m â€¢ n â€¢ x = (Completion.map (SMul.smul m) âˆ˜ Completion.map (SMul.smul n)) x := rfl
    -- âŠ¢ m â€¢ n â€¢ x = n â€¢ m â€¢ x
    have hnm : n â€¢ m â€¢ x = (Completion.map (SMul.smul n) âˆ˜ Completion.map (SMul.smul m)) x := rfl
    -- âŠ¢ m â€¢ n â€¢ x = n â€¢ m â€¢ x
    rw [hmn, hnm, map_comp, map_comp]
    exact congr_arg (fun f => Completion.map f x) (funext (smul_comm _ _))
    repeat' exact uniformContinuous_const_smul _âŸ©
    -- ğŸ‰ no goals

@[to_additive]
instance [SMul Máµáµ’áµ– X] [IsCentralScalar M X] : IsCentralScalar M (Completion X) :=
  âŸ¨fun c a => (congr_arg fun f => Completion.map f a) <| funext (op_smul_eq_smul c)âŸ©

variable {M X}
variable [UniformContinuousConstSMul M X]

@[to_additive (attr := simp, norm_cast)]
theorem coe_smul (c : M) (x : X) : (â†‘(c â€¢ x) : Completion X) = c â€¢ (x : Completion X) :=
  (map_coe (uniformContinuous_const_smul c) x).symm
#align uniform_space.completion.coe_smul UniformSpace.Completion.coe_smul
#align uniform_space.completion.coe_vadd UniformSpace.Completion.coe_vadd

end SMul

@[to_additive]
noncomputable instance [Monoid M] [MulAction M X] [UniformContinuousConstSMul M X] :
    MulAction M (Completion X) where
  smul := (Â· â€¢ Â·)
  one_smul := ext' (continuous_const_smul _) continuous_id fun a => by rw [â† coe_smul, one_smul]
                                                                       -- ğŸ‰ no goals
  mul_smul x y :=
    ext' (continuous_const_smul _) ((continuous_const_smul _).const_smul _) fun a => by
      simp only [â† coe_smul, mul_smul]
      -- ğŸ‰ no goals

end Completion

end UniformSpace
