/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Algebra.Group.Pi.Lemmas
import Mathlib.Algebra.GroupWithZero.Units.Equiv
import Mathlib.Topology.Algebra.Monoid
import Mathlib.Topology.Homeomorph.Lemmas

/-!
# Topological group with zero

In this file we define `HasContinuousInv‚ÇÄ` to be a mixin typeclass a type with `Inv` and
`Zero` (e.g., a `GroupWithZero`) such that `fun x ‚Ü¶ x‚Åª¬π` is continuous at all nonzero points. Any
normed (semi)field has this property. Currently the only example of `HasContinuousInv‚ÇÄ` in
`mathlib` which is not a normed field is the type `NNReal` (a.k.a. `‚Ñù‚â•0`) of nonnegative real
numbers.

Then we prove lemmas about continuity of `x ‚Ü¶ x‚Åª¬π` and `f / g` providing dot-style `*.inv‚ÇÄ` and
`*.div` operations on `Filter.Tendsto`, `ContinuousAt`, `ContinuousWithinAt`, `ContinuousOn`,
and `Continuous`. As a special case, we provide `*.div_const` operations that require only
`DivInvMonoid` and `ContinuousMul` instances.

All lemmas about `(‚Åª¬π)` use `inv‚ÇÄ` in their names because lemmas without `‚ÇÄ` are used for
`IsTopologicalGroup`s. We also use `'` in the typeclass name `HasContinuousInv‚ÇÄ` for the sake of
consistency of notation.

On a `GroupWithZero` with continuous multiplication, we also define left and right multiplication
as homeomorphisms.
-/
open Topology Filter Function

/-!
### A `DivInvMonoid` with continuous multiplication

If `G‚ÇÄ` is a `DivInvMonoid` with continuous `(*)`, then `(/y)` is continuous for any `y`. In this
section we prove lemmas that immediately follow from this fact providing `*.div_const` dot-style
operations on `Filter.Tendsto`, `ContinuousAt`, `ContinuousWithinAt`, `ContinuousOn`, and
`Continuous`.
-/


variable {Œ± Œ≤ G‚ÇÄ : Type*}

section DivConst

variable [DivInvMonoid G‚ÇÄ] [TopologicalSpace G‚ÇÄ] [ContinuousMul G‚ÇÄ] {f : Œ± ‚Üí G‚ÇÄ} {s : Set Œ±}
  {l : Filter Œ±}

theorem Filter.Tendsto.div_const {x : G‚ÇÄ} (hf : Tendsto f l (ùìù x)) (y : G‚ÇÄ) :
    Tendsto (fun a ‚Ü¶ f a / y) l (ùìù (x / y)) := by
  simpa only [div_eq_mul_inv] using hf.mul tendsto_const_nhds

variable [TopologicalSpace Œ±]

nonrec theorem ContinuousAt.div_const {a : Œ±} (hf : ContinuousAt f a) (y : G‚ÇÄ) :
    ContinuousAt (fun x ‚Ü¶ f x / y) a :=
  hf.div_const y

nonrec theorem ContinuousWithinAt.div_const {a} (hf : ContinuousWithinAt f s a) (y : G‚ÇÄ) :
    ContinuousWithinAt (fun x ‚Ü¶ f x / y) s a :=
  hf.div_const _

theorem ContinuousOn.div_const (hf : ContinuousOn f s) (y : G‚ÇÄ) :
    ContinuousOn (fun x ‚Ü¶ f x / y) s := by
  simpa only [div_eq_mul_inv] using hf.mul continuousOn_const

@[continuity, fun_prop]
theorem Continuous.div_const (hf : Continuous f) (y : G‚ÇÄ) : Continuous fun x ‚Ü¶ f x / y := by
  simpa only [div_eq_mul_inv] using hf.mul continuous_const

end DivConst

/-- A type with `0` and `Inv` such that `fun x ‚Ü¶ x‚Åª¬π` is continuous at all nonzero points. Any
normed (semi)field has this property. -/
class HasContinuousInv‚ÇÄ (G‚ÇÄ : Type*) [Zero G‚ÇÄ] [Inv G‚ÇÄ] [TopologicalSpace G‚ÇÄ] : Prop where
  /-- The map `fun x ‚Ü¶ x‚Åª¬π` is continuous at all nonzero points. -/
  continuousAt_inv‚ÇÄ : ‚àÄ ‚¶Éx : G‚ÇÄ‚¶Ñ, x ‚â† 0 ‚Üí ContinuousAt Inv.inv x

export HasContinuousInv‚ÇÄ (continuousAt_inv‚ÇÄ)

section Inv‚ÇÄ

variable [Zero G‚ÇÄ] [Inv G‚ÇÄ] [TopologicalSpace G‚ÇÄ] [HasContinuousInv‚ÇÄ G‚ÇÄ] {l : Filter Œ±} {f : Œ± ‚Üí G‚ÇÄ}
  {s : Set Œ±} {a : Œ±}

/-!
### Continuity of `fun x ‚Ü¶ x‚Åª¬π` at a non-zero point

We define `HasContinuousInv‚ÇÄ` to be a `GroupWithZero` such that the operation `x ‚Ü¶ x‚Åª¬π`
is continuous at all nonzero points. In this section we prove dot-style `*.inv‚ÇÄ` lemmas for
`Filter.Tendsto`, `ContinuousAt`, `ContinuousWithinAt`, `ContinuousOn`, and `Continuous`.
-/

theorem tendsto_inv‚ÇÄ {x : G‚ÇÄ} (hx : x ‚â† 0) : Tendsto Inv.inv (ùìù x) (ùìù x‚Åª¬π) :=
  continuousAt_inv‚ÇÄ hx

theorem continuousOn_inv‚ÇÄ : ContinuousOn (Inv.inv : G‚ÇÄ ‚Üí G‚ÇÄ) {0}·∂ú := fun _x hx ‚Ü¶
  (continuousAt_inv‚ÇÄ hx).continuousWithinAt

/-- If a function converges to a nonzero value, its inverse converges to the inverse of this value.
We use the name `Filter.Tendsto.inv‚ÇÄ` as `Filter.Tendsto.inv` is already used in multiplicative
topological groups. -/
theorem Filter.Tendsto.inv‚ÇÄ {a : G‚ÇÄ} (hf : Tendsto f l (ùìù a)) (ha : a ‚â† 0) :
    Tendsto (fun x ‚Ü¶ (f x)‚Åª¬π) l (ùìù a‚Åª¬π) :=
  (tendsto_inv‚ÇÄ ha).comp hf

variable [TopologicalSpace Œ±]

nonrec theorem ContinuousWithinAt.inv‚ÇÄ (hf : ContinuousWithinAt f s a) (ha : f a ‚â† 0) :
    ContinuousWithinAt (fun x ‚Ü¶ (f x)‚Åª¬π) s a :=
  hf.inv‚ÇÄ ha

@[fun_prop]
nonrec theorem ContinuousAt.inv‚ÇÄ (hf : ContinuousAt f a) (ha : f a ‚â† 0) :
    ContinuousAt (fun x ‚Ü¶ (f x)‚Åª¬π) a :=
  hf.inv‚ÇÄ ha

@[continuity, fun_prop]
theorem Continuous.inv‚ÇÄ (hf : Continuous f) (h0 : ‚àÄ x, f x ‚â† 0) : Continuous fun x ‚Ü¶ (f x)‚Åª¬π :=
  continuous_iff_continuousAt.2 fun x ‚Ü¶ (hf.tendsto x).inv‚ÇÄ (h0 x)

@[fun_prop]
theorem ContinuousOn.inv‚ÇÄ (hf : ContinuousOn f s) (h0 : ‚àÄ x ‚àà s, f x ‚â† 0) :
    ContinuousOn (fun x ‚Ü¶ (f x)‚Åª¬π) s := fun x hx ‚Ü¶ (hf x hx).inv‚ÇÄ (h0 x hx)

end Inv‚ÇÄ

section GroupWithZero

variable [GroupWithZero G‚ÇÄ] [TopologicalSpace G‚ÇÄ] [HasContinuousInv‚ÇÄ G‚ÇÄ]

/-- If `G‚ÇÄ` is a group with zero with topology such that `x ‚Ü¶ x‚Åª¬π` is continuous at all nonzero
points. Then the coercion `G‚ÇÄÀ£ ‚Üí G‚ÇÄ` is a topological embedding. -/
theorem Units.isEmbedding_val‚ÇÄ : IsEmbedding (val : G‚ÇÄÀ£ ‚Üí G‚ÇÄ) :=
  embedding_val_mk <| (continuousOn_inv‚ÇÄ (G‚ÇÄ := G‚ÇÄ)).mono fun _ ‚Ü¶ IsUnit.ne_zero

/-- If a group with zero has continuous inversion, then its group of units is homeomorphic to
the set of nonzero elements. -/
noncomputable def unitsHomeomorphNeZero : G‚ÇÄÀ£ ‚âÉ‚Çú {g : G‚ÇÄ // g ‚â† 0} :=
  Units.isEmbedding_val‚ÇÄ.toHomeomorph.trans <| show _ ‚âÉ‚Çú {g | _} from .setCongr <|
    Set.ext fun x ‚Ü¶ (Units.exists_iff_ne_zero (p := (¬∑ = x))).trans <| by simp

end GroupWithZero

section NhdsInv

variable [GroupWithZero G‚ÇÄ] [TopologicalSpace G‚ÇÄ] [HasContinuousInv‚ÇÄ G‚ÇÄ] {x : G‚ÇÄ}

lemma nhds_inv‚ÇÄ (hx : x ‚â† 0) : ùìù x‚Åª¬π = (ùìù x)‚Åª¬π := by
  refine le_antisymm (inv_le_iff_le_inv.1 ?_) (tendsto_inv‚ÇÄ hx)
  simpa only [inv_inv] using tendsto_inv‚ÇÄ (inv_ne_zero hx)

lemma tendsto_inv_iff‚ÇÄ {l : Filter Œ±} {f : Œ± ‚Üí G‚ÇÄ} (hx : x ‚â† 0) :
    Tendsto (fun x ‚Ü¶ (f x)‚Åª¬π) l (ùìù x‚Åª¬π) ‚Üî Tendsto f l (ùìù x) := by
  simp only [nhds_inv‚ÇÄ hx, ‚Üê Filter.comap_inv, tendsto_comap_iff, Function.comp_def, inv_inv]

end NhdsInv

/-!
### Continuity of division

If `G‚ÇÄ` is a `GroupWithZero` with `x ‚Ü¶ x‚Åª¬π` continuous at all nonzero points and `(*)`, then
division `(/)` is continuous at any point where the denominator is continuous.
-/

section Div

variable [GroupWithZero G‚ÇÄ] [TopologicalSpace G‚ÇÄ] [HasContinuousInv‚ÇÄ G‚ÇÄ] [ContinuousMul G‚ÇÄ]
  {f g : Œ± ‚Üí G‚ÇÄ}

theorem Filter.Tendsto.div {l : Filter Œ±} {a b : G‚ÇÄ} (hf : Tendsto f l (ùìù a))
    (hg : Tendsto g l (ùìù b)) (hy : b ‚â† 0) : Tendsto (f / g) l (ùìù (a / b)) := by
  simpa only [div_eq_mul_inv] using hf.mul (hg.inv‚ÇÄ hy)

theorem Filter.tendsto_mul_iff_of_ne_zero [T1Space G‚ÇÄ] {f g : Œ± ‚Üí G‚ÇÄ} {l : Filter Œ±} {x y : G‚ÇÄ}
    (hg : Tendsto g l (ùìù y)) (hy : y ‚â† 0) :
    Tendsto (fun n ‚Ü¶ f n * g n) l (ùìù <| x * y) ‚Üî Tendsto f l (ùìù x) := by
  refine ‚ü®fun hfg ‚Ü¶ ?_, fun hf ‚Ü¶ hf.mul hg‚ü©
  rw [‚Üê mul_div_cancel_right‚ÇÄ x hy]
  refine Tendsto.congr' ?_ (hfg.div hg hy)
  exact (hg.eventually_ne hy).mono fun n hn ‚Ü¶ mul_div_cancel_right‚ÇÄ _ hn

variable [TopologicalSpace Œ±] [TopologicalSpace Œ≤] {s : Set Œ±} {a : Œ±}

nonrec theorem ContinuousWithinAt.div (hf : ContinuousWithinAt f s a)
    (hg : ContinuousWithinAt g s a) (h‚ÇÄ : g a ‚â† 0) : ContinuousWithinAt (f / g) s a :=
  hf.div hg h‚ÇÄ

theorem ContinuousOn.div (hf : ContinuousOn f s) (hg : ContinuousOn g s) (h‚ÇÄ : ‚àÄ x ‚àà s, g x ‚â† 0) :
    ContinuousOn (f / g) s := fun x hx ‚Ü¶ (hf x hx).div (hg x hx) (h‚ÇÄ x hx)

/-- Continuity at a point of the result of dividing two functions continuous at that point, where
the denominator is nonzero. -/
nonrec theorem ContinuousAt.div (hf : ContinuousAt f a) (hg : ContinuousAt g a) (h‚ÇÄ : g a ‚â† 0) :
    ContinuousAt (f / g) a :=
  hf.div hg h‚ÇÄ

@[continuity]
theorem Continuous.div (hf : Continuous f) (hg : Continuous g) (h‚ÇÄ : ‚àÄ x, g x ‚â† 0) :
    Continuous (f / g) := by simpa only [div_eq_mul_inv] using hf.mul (hg.inv‚ÇÄ h‚ÇÄ)

theorem continuousOn_div : ContinuousOn (fun p : G‚ÇÄ √ó G‚ÇÄ ‚Ü¶ p.1 / p.2) { p | p.2 ‚â† 0 } :=
  continuousOn_fst.div continuousOn_snd fun _ ‚Ü¶ id

@[fun_prop]
theorem Continuous.div‚ÇÄ (hf : Continuous f) (hg : Continuous g) (h‚ÇÄ : ‚àÄ x, g x ‚â† 0) :
    Continuous (fun x ‚Ü¶ f x / g x) := by
  simpa only [div_eq_mul_inv] using hf.mul (hg.inv‚ÇÄ h‚ÇÄ)

@[fun_prop]
theorem ContinuousAt.div‚ÇÄ (hf : ContinuousAt f a) (hg : ContinuousAt g a) (h‚ÇÄ : g a ‚â† 0) :
    ContinuousAt (fun x ‚Ü¶ f x / g x) a := ContinuousAt.div hf hg h‚ÇÄ

@[fun_prop]
theorem ContinuousOn.div‚ÇÄ (hf : ContinuousOn f s) (hg : ContinuousOn g s) (h‚ÇÄ : ‚àÄ x ‚àà s, g x ‚â† 0) :
    ContinuousOn (fun x ‚Ü¶ f x / g x) s := ContinuousOn.div hf hg h‚ÇÄ

/-- The function `f x / g x` is discontinuous when `g x = 0`. However, under appropriate
conditions, `h x (f x / g x)` is still continuous.  The condition is that if `g a = 0` then `h x y`
must tend to `h a 0` when `x` tends to `a`, with no information about `y`. This is represented by
the `‚ä§` filter.  Note: `tendsto_prod_top_iff` characterizes this convergence in uniform spaces.  See
also `Filter.prod_top` and `Filter.mem_prod_top`. -/
theorem ContinuousAt.comp_div_cases {f g : Œ± ‚Üí G‚ÇÄ} (h : Œ± ‚Üí G‚ÇÄ ‚Üí Œ≤) (hf : ContinuousAt f a)
    (hg : ContinuousAt g a) (hh : g a ‚â† 0 ‚Üí ContinuousAt ‚Üøh (a, f a / g a))
    (h2h : g a = 0 ‚Üí Tendsto ‚Üøh (ùìù a √óÀ¢ ‚ä§) (ùìù (h a 0))) :
    ContinuousAt (fun x ‚Ü¶ h x (f x / g x)) a := by
  change ContinuousAt (‚Üøh ‚àò fun x ‚Ü¶ (x, f x / g x)) a
  by_cases hga : g a = 0
  ¬∑ rw [ContinuousAt]
    simp_rw [comp_apply, hga, div_zero]
    exact (h2h hga).comp (continuousAt_id.tendsto.prodMk tendsto_top)
  ¬∑ fun_prop (disch := assumption)

/-- `h x (f x / g x)` is continuous under certain conditions, even if the denominator is sometimes
  `0`. See docstring of `ContinuousAt.comp_div_cases`. -/
theorem Continuous.comp_div_cases {f g : Œ± ‚Üí G‚ÇÄ} (h : Œ± ‚Üí G‚ÇÄ ‚Üí Œ≤) (hf : Continuous f)
    (hg : Continuous g) (hh : ‚àÄ a, g a ‚â† 0 ‚Üí ContinuousAt ‚Üøh (a, f a / g a))
    (h2h : ‚àÄ a, g a = 0 ‚Üí Tendsto ‚Üøh (ùìù a √óÀ¢ ‚ä§) (ùìù (h a 0))) :
    Continuous fun x ‚Ü¶ h x (f x / g x) :=
  continuous_iff_continuousAt.mpr fun a ‚Ü¶
    hf.continuousAt.comp_div_cases _ hg.continuousAt (hh a) (h2h a)

end Div

/-! ### Left and right multiplication as homeomorphisms -/


namespace Homeomorph

variable [TopologicalSpace Œ±] [GroupWithZero Œ±] [ContinuousMul Œ±]

/-- Left multiplication by a nonzero element in a `GroupWithZero` with continuous multiplication
is a homeomorphism of the underlying type. -/
protected def mulLeft‚ÇÄ (c : Œ±) (hc : c ‚â† 0) : Œ± ‚âÉ‚Çú Œ± :=
  { Equiv.mulLeft‚ÇÄ c hc with
    continuous_toFun := continuous_mul_left _
    continuous_invFun := continuous_mul_left _ }

/-- Right multiplication by a nonzero element in a `GroupWithZero` with continuous multiplication
is a homeomorphism of the underlying type. -/
protected def mulRight‚ÇÄ (c : Œ±) (hc : c ‚â† 0) : Œ± ‚âÉ‚Çú Œ± :=
  { Equiv.mulRight‚ÇÄ c hc with
    continuous_toFun := continuous_mul_right _
    continuous_invFun := continuous_mul_right _ }

@[simp]
theorem coe_mulLeft‚ÇÄ (c : Œ±) (hc : c ‚â† 0) : ‚áë(Homeomorph.mulLeft‚ÇÄ c hc) = (c * ¬∑) :=
  rfl

@[simp]
theorem mulLeft‚ÇÄ_symm_apply (c : Œ±) (hc : c ‚â† 0) :
    ((Homeomorph.mulLeft‚ÇÄ c hc).symm : Œ± ‚Üí Œ±) = (c‚Åª¬π * ¬∑) :=
  rfl

@[simp]
theorem coe_mulRight‚ÇÄ (c : Œ±) (hc : c ‚â† 0) : ‚áë(Homeomorph.mulRight‚ÇÄ c hc) = (¬∑ * c) :=
  rfl

@[simp]
theorem mulRight‚ÇÄ_symm_apply (c : Œ±) (hc : c ‚â† 0) :
    ((Homeomorph.mulRight‚ÇÄ c hc).symm : Œ± ‚Üí Œ±) = (¬∑ * c‚Åª¬π) :=
  rfl

end Homeomorph

section map_comap

variable [TopologicalSpace G‚ÇÄ] [GroupWithZero G‚ÇÄ] [ContinuousMul G‚ÇÄ] {a : G‚ÇÄ}

theorem map_mul_left_nhds‚ÇÄ (ha : a ‚â† 0) (b : G‚ÇÄ) : map (a * ¬∑) (ùìù b) = ùìù (a * b) :=
  (Homeomorph.mulLeft‚ÇÄ a ha).map_nhds_eq b

theorem map_mul_left_nhds_one‚ÇÄ (ha : a ‚â† 0) : map (a * ¬∑) (ùìù 1) = ùìù (a) := by
  rw [map_mul_left_nhds‚ÇÄ ha, mul_one]

theorem map_mul_right_nhds‚ÇÄ (ha : a ‚â† 0) (b : G‚ÇÄ) : map (¬∑ * a) (ùìù b) = ùìù (b * a) :=
  (Homeomorph.mulRight‚ÇÄ a ha).map_nhds_eq b

theorem map_mul_right_nhds_one‚ÇÄ (ha : a ‚â† 0) : map (¬∑ * a) (ùìù 1) = ùìù (a) := by
  rw [map_mul_right_nhds‚ÇÄ ha, one_mul]

theorem nhds_translation_mul_inv‚ÇÄ (ha : a ‚â† 0) : comap (¬∑ * a‚Åª¬π) (ùìù 1) = ùìù a :=
  ((Homeomorph.mulRight‚ÇÄ a ha).symm.comap_nhds_eq 1).trans <| by simp

/-- If a group with zero has continuous multiplication and `fun x ‚Ü¶ x‚Åª¬π` is continuous at one,
then it is continuous at any unit. -/
theorem HasContinuousInv‚ÇÄ.of_nhds_one (h : Tendsto Inv.inv (ùìù (1 : G‚ÇÄ)) (ùìù 1)) :
    HasContinuousInv‚ÇÄ G‚ÇÄ where
  continuousAt_inv‚ÇÄ x hx := by
    have hx' := inv_ne_zero hx
    rw [ContinuousAt, ‚Üê map_mul_left_nhds_one‚ÇÄ hx, ‚Üê nhds_translation_mul_inv‚ÇÄ hx',
      tendsto_map'_iff, tendsto_comap_iff]
    simpa only [Function.comp_def, mul_inv_rev, mul_inv_cancel_right‚ÇÄ hx']

end map_comap

section ZPow

variable [GroupWithZero G‚ÇÄ] [TopologicalSpace G‚ÇÄ] [HasContinuousInv‚ÇÄ G‚ÇÄ] [ContinuousMul G‚ÇÄ]

theorem continuousAt_zpow‚ÇÄ (x : G‚ÇÄ) (m : ‚Ñ§) (h : x ‚â† 0 ‚à® 0 ‚â§ m) :
    ContinuousAt (fun x ‚Ü¶ x ^ m) x := by
  rcases m with m | m
  ¬∑ simpa only [Int.ofNat_eq_coe, zpow_natCast] using continuousAt_pow x m
  ¬∑ simp only [zpow_negSucc]
    have hx : x ‚â† 0 := h.resolve_right (Int.negSucc_lt_zero m).not_ge
    exact (continuousAt_pow x (m + 1)).inv‚ÇÄ (pow_ne_zero _ hx)

theorem continuousOn_zpow‚ÇÄ (m : ‚Ñ§) : ContinuousOn (fun x : G‚ÇÄ ‚Ü¶ x ^ m) {0}·∂ú := fun _x hx ‚Ü¶
  (continuousAt_zpow‚ÇÄ _ _ (Or.inl hx)).continuousWithinAt

theorem Filter.Tendsto.zpow‚ÇÄ {f : Œ± ‚Üí G‚ÇÄ} {l : Filter Œ±} {a : G‚ÇÄ} (hf : Tendsto f l (ùìù a)) (m : ‚Ñ§)
    (h : a ‚â† 0 ‚à® 0 ‚â§ m) : Tendsto (fun x ‚Ü¶ f x ^ m) l (ùìù (a ^ m)) :=
  (continuousAt_zpow‚ÇÄ _ m h).tendsto.comp hf

variable {X : Type*} [TopologicalSpace X] {a : X} {s : Set X} {f : X ‚Üí G‚ÇÄ}

@[fun_prop]
nonrec theorem ContinuousAt.zpow‚ÇÄ (hf : ContinuousAt f a) (m : ‚Ñ§) (h : f a ‚â† 0 ‚à® 0 ‚â§ m) :
    ContinuousAt (fun x ‚Ü¶ f x ^ m) a :=
  hf.zpow‚ÇÄ m h

nonrec theorem ContinuousWithinAt.zpow‚ÇÄ (hf : ContinuousWithinAt f s a) (m : ‚Ñ§)
    (h : f a ‚â† 0 ‚à® 0 ‚â§ m) : ContinuousWithinAt (fun x ‚Ü¶ f x ^ m) s a :=
  hf.zpow‚ÇÄ m h

@[fun_prop]
theorem ContinuousOn.zpow‚ÇÄ (hf : ContinuousOn f s) (m : ‚Ñ§) (h : ‚àÄ a ‚àà s, f a ‚â† 0 ‚à® 0 ‚â§ m) :
    ContinuousOn (fun x ‚Ü¶ f x ^ m) s := fun a ha ‚Ü¶ (hf a ha).zpow‚ÇÄ m (h a ha)

@[continuity, fun_prop]
theorem Continuous.zpow‚ÇÄ (hf : Continuous f) (m : ‚Ñ§) (h0 : ‚àÄ a, f a ‚â† 0 ‚à® 0 ‚â§ m) :
    Continuous fun x ‚Ü¶ f x ^ m :=
  continuous_iff_continuousAt.2 fun x ‚Ü¶ (hf.tendsto x).zpow‚ÇÄ m (h0 x)

end ZPow
