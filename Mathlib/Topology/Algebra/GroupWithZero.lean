/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Algebra.Group.Pi.Lemmas
import Mathlib.Algebra.GroupWithZero.Units.Equiv
import Mathlib.Topology.Algebra.Monoid
import Mathlib.Topology.Homeomorph.Lemmas

/-!
# Topological group with zero

In this file we define `ContinuousInvâ‚€` to be a mixin typeclass a type with `Inv` and
`Zero` (e.g., a `GroupWithZero`) such that `fun x â†¦ xâ»Â¹` is continuous at all nonzero points. Any
normed (semi)field has this property. Currently the only example of `ContinuousInvâ‚€` in
`mathlib` which is not a normed field is the type `NNReal` (a.k.a. `â„â‰¥0`) of nonnegative real
numbers.

Then we prove lemmas about continuity of `x â†¦ xâ»Â¹` and `f / g` providing dot-style `*.invâ‚€` and
`*.div` operations on `Filter.Tendsto`, `ContinuousAt`, `ContinuousWithinAt`, `ContinuousOn`,
and `Continuous`. As a special case, we provide `*.div_const` operations that require only
`DivInvMonoid` and `ContinuousMul` instances.

All lemmas about `(â»Â¹)` use `invâ‚€` in their names because lemmas without `â‚€` are used for
`IsTopologicalGroup`s. We also use `'` in the typeclass name `ContinuousInvâ‚€` for the sake of
consistency of notation.

On a `GroupWithZero` with continuous multiplication, we also define left and right multiplication
as homeomorphisms.
-/
open Topology Filter Function

/-!
### A `DivInvMonoid` with continuous multiplication

If `Gâ‚€` is a `DivInvMonoid` with continuous `(*)`, then `(/y)` is continuous for any `y`. In this
section we prove lemmas that immediately follow from this fact providing `*.div_const` dot-style
operations on `Filter.Tendsto`, `ContinuousAt`, `ContinuousWithinAt`, `ContinuousOn`, and
`Continuous`.
-/


variable {Î± Î² Gâ‚€ : Type*}

section DivConst

variable [DivInvMonoid Gâ‚€] [TopologicalSpace Gâ‚€] [ContinuousMul Gâ‚€] {f : Î± â†’ Gâ‚€} {s : Set Î±}
  {l : Filter Î±}

theorem Filter.Tendsto.div_const {x : Gâ‚€} (hf : Tendsto f l (ğ“ x)) (y : Gâ‚€) :
    Tendsto (fun a => f a / y) l (ğ“ (x / y)) := by
  simpa only [div_eq_mul_inv] using hf.mul tendsto_const_nhds

variable [TopologicalSpace Î±]

nonrec theorem ContinuousAt.div_const {a : Î±} (hf : ContinuousAt f a) (y : Gâ‚€) :
    ContinuousAt (fun x => f x / y) a :=
  hf.div_const y

nonrec theorem ContinuousWithinAt.div_const {a} (hf : ContinuousWithinAt f s a) (y : Gâ‚€) :
    ContinuousWithinAt (fun x => f x / y) s a :=
  hf.div_const _

theorem ContinuousOn.div_const (hf : ContinuousOn f s) (y : Gâ‚€) :
    ContinuousOn (fun x => f x / y) s := by
  simpa only [div_eq_mul_inv] using hf.mul continuousOn_const

@[continuity, fun_prop]
theorem Continuous.div_const (hf : Continuous f) (y : Gâ‚€) : Continuous fun x => f x / y := by
  simpa only [div_eq_mul_inv] using hf.mul continuous_const

end DivConst

/-- A type with `0` and `Inv` such that `fun x â†¦ xâ»Â¹` is continuous at all nonzero points. Any
normed (semi)field has this property. -/
class ContinuousInvâ‚€ (Gâ‚€ : Type*) [Zero Gâ‚€] [Inv Gâ‚€] [TopologicalSpace Gâ‚€] : Prop where
  /-- The map `fun x â†¦ xâ»Â¹` is continuous at all nonzero points. -/
  continuousAt_invâ‚€ : âˆ€ â¦ƒx : Gâ‚€â¦„, x â‰  0 â†’ ContinuousAt Inv.inv x

export ContinuousInvâ‚€ (continuousAt_invâ‚€)

@[deprecated (since := "2025-09-01")] alias HasContinuousInvâ‚€ := ContinuousInvâ‚€

section Invâ‚€

variable [Zero Gâ‚€] [Inv Gâ‚€] [TopologicalSpace Gâ‚€] [ContinuousInvâ‚€ Gâ‚€] {l : Filter Î±} {f : Î± â†’ Gâ‚€}
  {s : Set Î±} {a : Î±}

/-!
### Continuity of `fun x â†¦ xâ»Â¹` at a non-zero point

We define `ContinuousInvâ‚€` to be a `GroupWithZero` such that the operation `x â†¦ xâ»Â¹`
is continuous at all nonzero points. In this section we prove dot-style `*.invâ‚€` lemmas for
`Filter.Tendsto`, `ContinuousAt`, `ContinuousWithinAt`, `ContinuousOn`, and `Continuous`.
-/

theorem tendsto_invâ‚€ {x : Gâ‚€} (hx : x â‰  0) : Tendsto Inv.inv (ğ“ x) (ğ“ xâ»Â¹) :=
  continuousAt_invâ‚€ hx

theorem continuousOn_invâ‚€ : ContinuousOn (Inv.inv : Gâ‚€ â†’ Gâ‚€) {0}á¶œ := fun _x hx =>
  (continuousAt_invâ‚€ hx).continuousWithinAt

/-- If a function converges to a nonzero value, its inverse converges to the inverse of this value.
We use the name `Filter.Tendsto.invâ‚€` as `Filter.Tendsto.inv` is already used in multiplicative
topological groups. -/
theorem Filter.Tendsto.invâ‚€ {a : Gâ‚€} (hf : Tendsto f l (ğ“ a)) (ha : a â‰  0) :
    Tendsto (fun x => (f x)â»Â¹) l (ğ“ aâ»Â¹) :=
  (tendsto_invâ‚€ ha).comp hf

variable [TopologicalSpace Î±]

nonrec theorem ContinuousWithinAt.invâ‚€ (hf : ContinuousWithinAt f s a) (ha : f a â‰  0) :
    ContinuousWithinAt (fun x => (f x)â»Â¹) s a :=
  hf.invâ‚€ ha

@[fun_prop]
nonrec theorem ContinuousAt.invâ‚€ (hf : ContinuousAt f a) (ha : f a â‰  0) :
    ContinuousAt (fun x => (f x)â»Â¹) a :=
  hf.invâ‚€ ha

@[continuity, fun_prop]
theorem Continuous.invâ‚€ (hf : Continuous f) (h0 : âˆ€ x, f x â‰  0) : Continuous fun x => (f x)â»Â¹ :=
  continuous_iff_continuousAt.2 fun x => (hf.tendsto x).invâ‚€ (h0 x)

@[fun_prop]
theorem ContinuousOn.invâ‚€ (hf : ContinuousOn f s) (h0 : âˆ€ x âˆˆ s, f x â‰  0) :
    ContinuousOn (fun x => (f x)â»Â¹) s := fun x hx => (hf x hx).invâ‚€ (h0 x hx)

end Invâ‚€

section GroupWithZero

variable [GroupWithZero Gâ‚€] [TopologicalSpace Gâ‚€] [ContinuousInvâ‚€ Gâ‚€]

/-- If `Gâ‚€` is a group with zero with topology such that `x â†¦ xâ»Â¹` is continuous at all nonzero
points. Then the coercion `Gâ‚€Ë£ â†’ Gâ‚€` is a topological embedding. -/
theorem Units.isEmbedding_valâ‚€ : IsEmbedding (val : Gâ‚€Ë£ â†’ Gâ‚€) :=
  embedding_val_mk <| (continuousOn_invâ‚€ (Gâ‚€ := Gâ‚€)).mono fun _ â†¦ IsUnit.ne_zero

/-- If a group with zero has continuous inversion, then its group of units is homeomorphic to
the set of nonzero elements. -/
noncomputable def unitsHomeomorphNeZero : Gâ‚€Ë£ â‰ƒâ‚œ {g : Gâ‚€ // g â‰  0} :=
  Units.isEmbedding_valâ‚€.toHomeomorph.trans <| show _ â‰ƒâ‚œ {g | _} from .setCongr <|
    Set.ext fun x â†¦ (Units.exists_iff_ne_zero (p := (Â· = x))).trans <| by simp

end GroupWithZero

section NhdsInv

variable [GroupWithZero Gâ‚€] [TopologicalSpace Gâ‚€] [ContinuousInvâ‚€ Gâ‚€] {x : Gâ‚€}

lemma nhds_invâ‚€ (hx : x â‰  0) : ğ“ xâ»Â¹ = (ğ“ x)â»Â¹ := by
  refine le_antisymm (inv_le_iff_le_inv.1 ?_) (tendsto_invâ‚€ hx)
  simpa only [inv_inv] using tendsto_invâ‚€ (inv_ne_zero hx)

lemma tendsto_inv_iffâ‚€ {l : Filter Î±} {f : Î± â†’ Gâ‚€} (hx : x â‰  0) :
    Tendsto (fun x â†¦ (f x)â»Â¹) l (ğ“ xâ»Â¹) â†” Tendsto f l (ğ“ x) := by
  simp only [nhds_invâ‚€ hx, â† Filter.comap_inv, tendsto_comap_iff, Function.comp_def, inv_inv]

end NhdsInv

/-!
### Continuity of division

If `Gâ‚€` is a `GroupWithZero` with `x â†¦ xâ»Â¹` continuous at all nonzero points and `(*)`, then
division `(/)` is continuous at any point where the denominator is continuous.
-/

section Div

variable [GroupWithZero Gâ‚€] [TopologicalSpace Gâ‚€] [ContinuousInvâ‚€ Gâ‚€] [ContinuousMul Gâ‚€]
  {f g : Î± â†’ Gâ‚€}

theorem Filter.Tendsto.div {l : Filter Î±} {a b : Gâ‚€} (hf : Tendsto f l (ğ“ a))
    (hg : Tendsto g l (ğ“ b)) (hy : b â‰  0) : Tendsto (f / g) l (ğ“ (a / b)) := by
  simpa only [div_eq_mul_inv] using hf.mul (hg.invâ‚€ hy)

theorem Filter.tendsto_mul_iff_of_ne_zero [T1Space Gâ‚€] {f g : Î± â†’ Gâ‚€} {l : Filter Î±} {x y : Gâ‚€}
    (hg : Tendsto g l (ğ“ y)) (hy : y â‰  0) :
    Tendsto (fun n => f n * g n) l (ğ“ <| x * y) â†” Tendsto f l (ğ“ x) := by
  refine âŸ¨fun hfg => ?_, fun hf => hf.mul hgâŸ©
  rw [â† mul_div_cancel_rightâ‚€ x hy]
  refine Tendsto.congr' ?_ (hfg.div hg hy)
  exact (hg.eventually_ne hy).mono fun n hn => mul_div_cancel_rightâ‚€ _ hn

variable [TopologicalSpace Î±] [TopologicalSpace Î²] {s : Set Î±} {a : Î±}

nonrec theorem ContinuousWithinAt.div (hf : ContinuousWithinAt f s a)
    (hg : ContinuousWithinAt g s a) (hâ‚€ : g a â‰  0) : ContinuousWithinAt (f / g) s a :=
  hf.div hg hâ‚€

theorem ContinuousOn.div (hf : ContinuousOn f s) (hg : ContinuousOn g s) (hâ‚€ : âˆ€ x âˆˆ s, g x â‰  0) :
    ContinuousOn (f / g) s := fun x hx => (hf x hx).div (hg x hx) (hâ‚€ x hx)

/-- Continuity at a point of the result of dividing two functions continuous at that point, where
the denominator is nonzero. -/
nonrec theorem ContinuousAt.div (hf : ContinuousAt f a) (hg : ContinuousAt g a) (hâ‚€ : g a â‰  0) :
    ContinuousAt (f / g) a :=
  hf.div hg hâ‚€

@[continuity]
theorem Continuous.div (hf : Continuous f) (hg : Continuous g) (hâ‚€ : âˆ€ x, g x â‰  0) :
    Continuous (f / g) := by simpa only [div_eq_mul_inv] using hf.mul (hg.invâ‚€ hâ‚€)

theorem continuousOn_div : ContinuousOn (fun p : Gâ‚€ Ã— Gâ‚€ => p.1 / p.2) { p | p.2 â‰  0 } :=
  continuousOn_fst.div continuousOn_snd fun _ => id

@[fun_prop]
theorem Continuous.divâ‚€ (hf : Continuous f) (hg : Continuous g) (hâ‚€ : âˆ€ x, g x â‰  0) :
    Continuous (fun x => f x / g x) := by
  simpa only [div_eq_mul_inv] using hf.mul (hg.invâ‚€ hâ‚€)

@[fun_prop]
theorem ContinuousAt.divâ‚€ (hf : ContinuousAt f a) (hg : ContinuousAt g a) (hâ‚€ : g a â‰  0) :
    ContinuousAt (fun x => f x / g x) a := ContinuousAt.div hf hg hâ‚€

@[fun_prop]
theorem ContinuousOn.divâ‚€ (hf : ContinuousOn f s) (hg : ContinuousOn g s) (hâ‚€ : âˆ€ x âˆˆ s, g x â‰  0) :
    ContinuousOn (fun x => f x / g x) s := ContinuousOn.div hf hg hâ‚€

/-- The function `f x / g x` is discontinuous when `g x = 0`. However, under appropriate
conditions, `h x (f x / g x)` is still continuous.  The condition is that if `g a = 0` then `h x y`
must tend to `h a 0` when `x` tends to `a`, with no information about `y`. This is represented by
the `âŠ¤` filter.  Note: `tendsto_prod_top_iff` characterizes this convergence in uniform spaces.  See
also `Filter.prod_top` and `Filter.mem_prod_top`. -/
theorem ContinuousAt.comp_div_cases {f g : Î± â†’ Gâ‚€} (h : Î± â†’ Gâ‚€ â†’ Î²) (hf : ContinuousAt f a)
    (hg : ContinuousAt g a) (hh : g a â‰  0 â†’ ContinuousAt â†¿h (a, f a / g a))
    (h2h : g a = 0 â†’ Tendsto â†¿h (ğ“ a Ã—Ë¢ âŠ¤) (ğ“ (h a 0))) :
    ContinuousAt (fun x => h x (f x / g x)) a := by
  change ContinuousAt (â†¿h âˆ˜ fun x => (x, f x / g x)) a
  by_cases hga : g a = 0
  Â· rw [ContinuousAt]
    simp_rw [comp_apply, hga, div_zero]
    exact (h2h hga).comp (continuousAt_id.tendsto.prodMk tendsto_top)
  Â· fun_prop (disch := assumption)

/-- `h x (f x / g x)` is continuous under certain conditions, even if the denominator is sometimes
  `0`. See docstring of `ContinuousAt.comp_div_cases`. -/
theorem Continuous.comp_div_cases {f g : Î± â†’ Gâ‚€} (h : Î± â†’ Gâ‚€ â†’ Î²) (hf : Continuous f)
    (hg : Continuous g) (hh : âˆ€ a, g a â‰  0 â†’ ContinuousAt â†¿h (a, f a / g a))
    (h2h : âˆ€ a, g a = 0 â†’ Tendsto â†¿h (ğ“ a Ã—Ë¢ âŠ¤) (ğ“ (h a 0))) :
    Continuous fun x => h x (f x / g x) :=
  continuous_iff_continuousAt.mpr fun a =>
    hf.continuousAt.comp_div_cases _ hg.continuousAt (hh a) (h2h a)

end Div

/-! ### Left and right multiplication as homeomorphisms -/


namespace Homeomorph

variable [TopologicalSpace Î±] [GroupWithZero Î±] [ContinuousMul Î±]

/-- Left multiplication by a nonzero element in a `GroupWithZero` with continuous multiplication
is a homeomorphism of the underlying type. -/
protected def mulLeftâ‚€ (c : Î±) (hc : c â‰  0) : Î± â‰ƒâ‚œ Î± :=
  { Equiv.mulLeftâ‚€ c hc with
    continuous_toFun := continuous_mul_left _
    continuous_invFun := continuous_mul_left _ }

/-- Right multiplication by a nonzero element in a `GroupWithZero` with continuous multiplication
is a homeomorphism of the underlying type. -/
protected def mulRightâ‚€ (c : Î±) (hc : c â‰  0) : Î± â‰ƒâ‚œ Î± :=
  { Equiv.mulRightâ‚€ c hc with
    continuous_toFun := continuous_mul_right _
    continuous_invFun := continuous_mul_right _ }

@[simp]
theorem coe_mulLeftâ‚€ (c : Î±) (hc : c â‰  0) : â‡‘(Homeomorph.mulLeftâ‚€ c hc) = (c * Â·) :=
  rfl

@[simp]
theorem mulLeftâ‚€_symm_apply (c : Î±) (hc : c â‰  0) :
    ((Homeomorph.mulLeftâ‚€ c hc).symm : Î± â†’ Î±) = (câ»Â¹ * Â·) :=
  rfl

@[simp]
theorem coe_mulRightâ‚€ (c : Î±) (hc : c â‰  0) : â‡‘(Homeomorph.mulRightâ‚€ c hc) = (Â· * c) :=
  rfl

@[simp]
theorem mulRightâ‚€_symm_apply (c : Î±) (hc : c â‰  0) :
    ((Homeomorph.mulRightâ‚€ c hc).symm : Î± â†’ Î±) = (Â· * câ»Â¹) :=
  rfl

end Homeomorph

section map_comap

variable [TopologicalSpace Gâ‚€] [GroupWithZero Gâ‚€] [ContinuousMul Gâ‚€] {a : Gâ‚€}

theorem map_mul_left_nhdsâ‚€ (ha : a â‰  0) (b : Gâ‚€) : map (a * Â·) (ğ“ b) = ğ“ (a * b) :=
  (Homeomorph.mulLeftâ‚€ a ha).map_nhds_eq b

theorem map_mul_left_nhds_oneâ‚€ (ha : a â‰  0) : map (a * Â·) (ğ“ 1) = ğ“ (a) := by
  rw [map_mul_left_nhdsâ‚€ ha, mul_one]

theorem map_mul_right_nhdsâ‚€ (ha : a â‰  0) (b : Gâ‚€) : map (Â· * a) (ğ“ b) = ğ“ (b * a) :=
  (Homeomorph.mulRightâ‚€ a ha).map_nhds_eq b

theorem map_mul_right_nhds_oneâ‚€ (ha : a â‰  0) : map (Â· * a) (ğ“ 1) = ğ“ (a) := by
  rw [map_mul_right_nhdsâ‚€ ha, one_mul]

theorem nhds_translation_mul_invâ‚€ (ha : a â‰  0) : comap (Â· * aâ»Â¹) (ğ“ 1) = ğ“ a :=
  ((Homeomorph.mulRightâ‚€ a ha).symm.comap_nhds_eq 1).trans <| by simp

/-- If a group with zero has continuous multiplication and `fun x â†¦ xâ»Â¹` is continuous at one,
then it is continuous at any unit. -/
theorem ContinuousInvâ‚€.of_nhds_one (h : Tendsto Inv.inv (ğ“ (1 : Gâ‚€)) (ğ“ 1)) :
    ContinuousInvâ‚€ Gâ‚€ where
  continuousAt_invâ‚€ x hx := by
    have hx' := inv_ne_zero hx
    rw [ContinuousAt, â† map_mul_left_nhds_oneâ‚€ hx, â† nhds_translation_mul_invâ‚€ hx',
      tendsto_map'_iff, tendsto_comap_iff]
    simpa only [Function.comp_def, mul_inv_rev, mul_inv_cancel_rightâ‚€ hx']

@[deprecated (since := "2025-09-01")] alias HasContinuousInvâ‚€.of_nhds_one :=
  ContinuousInvâ‚€.of_nhds_one

end map_comap

section ZPow

variable [GroupWithZero Gâ‚€] [TopologicalSpace Gâ‚€] [ContinuousInvâ‚€ Gâ‚€] [ContinuousMul Gâ‚€]

theorem continuousAt_zpowâ‚€ (x : Gâ‚€) (m : â„¤) (h : x â‰  0 âˆ¨ 0 â‰¤ m) :
    ContinuousAt (fun x => x ^ m) x := by
  rcases m with m | m
  Â· simpa only [Int.ofNat_eq_natCast, zpow_natCast] using continuousAt_pow x m
  Â· simp only [zpow_negSucc]
    have hx : x â‰  0 := h.resolve_right (Int.negSucc_lt_zero m).not_ge
    exact (continuousAt_pow x (m + 1)).invâ‚€ (pow_ne_zero _ hx)

theorem continuousOn_zpowâ‚€ (m : â„¤) : ContinuousOn (fun x : Gâ‚€ => x ^ m) {0}á¶œ := fun _x hx =>
  (continuousAt_zpowâ‚€ _ _ (Or.inl hx)).continuousWithinAt

theorem Filter.Tendsto.zpowâ‚€ {f : Î± â†’ Gâ‚€} {l : Filter Î±} {a : Gâ‚€} (hf : Tendsto f l (ğ“ a)) (m : â„¤)
    (h : a â‰  0 âˆ¨ 0 â‰¤ m) : Tendsto (fun x => f x ^ m) l (ğ“ (a ^ m)) :=
  (continuousAt_zpowâ‚€ _ m h).tendsto.comp hf

variable {X : Type*} [TopologicalSpace X] {a : X} {s : Set X} {f : X â†’ Gâ‚€}

@[fun_prop]
nonrec theorem ContinuousAt.zpowâ‚€ (hf : ContinuousAt f a) (m : â„¤) (h : f a â‰  0 âˆ¨ 0 â‰¤ m) :
    ContinuousAt (fun x => f x ^ m) a :=
  hf.zpowâ‚€ m h

nonrec theorem ContinuousWithinAt.zpowâ‚€ (hf : ContinuousWithinAt f s a) (m : â„¤)
    (h : f a â‰  0 âˆ¨ 0 â‰¤ m) : ContinuousWithinAt (fun x => f x ^ m) s a :=
  hf.zpowâ‚€ m h

@[fun_prop]
theorem ContinuousOn.zpowâ‚€ (hf : ContinuousOn f s) (m : â„¤) (h : âˆ€ a âˆˆ s, f a â‰  0 âˆ¨ 0 â‰¤ m) :
    ContinuousOn (fun x => f x ^ m) s := fun a ha => (hf a ha).zpowâ‚€ m (h a ha)

@[continuity, fun_prop]
theorem Continuous.zpowâ‚€ (hf : Continuous f) (m : â„¤) (h0 : âˆ€ a, f a â‰  0 âˆ¨ 0 â‰¤ m) :
    Continuous fun x => f x ^ m :=
  continuous_iff_continuousAt.2 fun x => (hf.tendsto x).zpowâ‚€ m (h0 x)

end ZPow
