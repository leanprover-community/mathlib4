/-
Copyright (c) 2021 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Kim Morrison
-/
import Mathlib.Algebra.Field.Subfield.Defs
import Mathlib.Algebra.GroupWithZero.Divisibility
import Mathlib.Algebra.Order.Group.Pointwise.Interval
import Mathlib.Topology.Algebra.GroupWithZero
import Mathlib.Topology.Algebra.Ring.Basic
import Mathlib.Topology.Order.LocalExtr

/-!
# Topological fields

A topological division ring is a topological ring whose inversion function is continuous at every
non-zero element.

-/

variable {K : Type*} [DivisionRing K] [TopologicalSpace K]

/-- Left-multiplication by a nonzero element of a topological division ring is proper, i.e.,
inverse images of compact sets are compact. -/
theorem Filter.tendsto_cocompact_mul_left‚ÇÄ [ContinuousMul K] {a : K} (ha : a ‚â† 0) :
    Filter.Tendsto (fun x : K ‚Ü¶ a * x) (Filter.cocompact K) (Filter.cocompact K) :=
  Filter.tendsto_cocompact_mul_left (inv_mul_cancel‚ÇÄ ha)

/-- Right-multiplication by a nonzero element of a topological division ring is proper, i.e.,
inverse images of compact sets are compact. -/
theorem Filter.tendsto_cocompact_mul_right‚ÇÄ [ContinuousMul K] {a : K} (ha : a ‚â† 0) :
    Filter.Tendsto (fun x : K ‚Ü¶ x * a) (Filter.cocompact K) (Filter.cocompact K) :=
  Filter.tendsto_cocompact_mul_right (mul_inv_cancel‚ÇÄ ha)

/-- Compact Hausdorff topological fields are finite. -/
instance (priority := 100) {K} [DivisionRing K] [TopologicalSpace K]
    [IsTopologicalRing K] [CompactSpace K] [T2Space K] : Finite K := by
  suffices DiscreteTopology K by
    exact finite_of_compact_of_discrete
  rw [discreteTopology_iff_isOpen_singleton_zero]
  exact GroupWithZero.isOpen_singleton_zero

variable (K)

/-- A topological division ring is a division ring with a topology where all operations are
continuous, including inversion. -/
class IsTopologicalDivisionRing : Prop extends IsTopologicalRing K, HasContinuousInv‚ÇÄ K

@[deprecated (since := "2025-03-25")] alias TopologicalDivisionRing := IsTopologicalDivisionRing

section Subfield

variable {Œ± : Type*} [Field Œ±] [TopologicalSpace Œ±] [IsTopologicalDivisionRing Œ±]

/-- The (topological-space) closure of a subfield of a topological field is
itself a subfield. -/
def Subfield.topologicalClosure (K : Subfield Œ±) : Subfield Œ± :=
  { K.toSubring.topologicalClosure with
    carrier := _root_.closure (K : Set Œ±)
    inv_mem' := fun x hx ‚Ü¶ by
      rcases eq_or_ne x 0 with (rfl | h)
      ¬∑ rwa [inv_zero]
      ¬∑ rw [‚Üê inv_coe_set, ‚Üê Set.image_inv_eq_inv]
        exact mem_closure_image (continuousAt_inv‚ÇÄ h) hx }

theorem Subfield.le_topologicalClosure (s : Subfield Œ±) : s ‚â§ s.topologicalClosure :=
  _root_.subset_closure

theorem Subfield.isClosed_topologicalClosure (s : Subfield Œ±) :
    IsClosed (s.topologicalClosure : Set Œ±) :=
  isClosed_closure

theorem Subfield.topologicalClosure_minimal (s : Subfield Œ±) {t : Subfield Œ±} (h : s ‚â§ t)
    (ht : IsClosed (t : Set Œ±)) : s.topologicalClosure ‚â§ t :=
  closure_minimal h ht

end Subfield

section affineHomeomorph

/-!
This section is about affine homeomorphisms from a topological field `ùïú` to itself.
Technically it does not require `ùïú` to be a topological field, a topological ring that
happens to be a field is enough.
-/


variable {ùïú : Type*} [Field ùïú] [TopologicalSpace ùïú] [IsTopologicalRing ùïú]

/--
The map `fun x ‚Ü¶ a * x + b`, as a homeomorphism from `ùïú` (a topological field) to itself,
when `a ‚â† 0`.
-/
@[simps]
def affineHomeomorph (a b : ùïú) (h : a ‚â† 0) : ùïú ‚âÉ‚Çú ùïú where
  toFun x := a * x + b
  invFun y := (y - b) / a
  left_inv x := by
    simp only [add_sub_cancel_right]
    exact mul_div_cancel_left‚ÇÄ x h
  right_inv y := by simp [mul_div_cancel‚ÇÄ _ h]

theorem affineHomeomorph_image_Icc {ùïú : Type*}
    [Field ùïú] [LinearOrder ùïú] [IsStrictOrderedRing ùïú] [TopologicalSpace ùïú]
    [IsTopologicalRing ùïú] (a b c d : ùïú) (h : 0 < a) :
    affineHomeomorph a b h.ne' '' Set.Icc c d = Set.Icc (a * c + b) (a * d + b) := by
  simp [h]

theorem affineHomeomorph_image_Ico {ùïú : Type*}
    [Field ùïú] [LinearOrder ùïú] [IsStrictOrderedRing ùïú] [TopologicalSpace ùïú]
    [IsTopologicalRing ùïú] (a b c d : ùïú) (h : 0 < a) :
    affineHomeomorph a b h.ne' '' Set.Ico c d = Set.Ico (a * c + b) (a * d + b) := by
  simp [h]

theorem affineHomeomorph_image_Ioc {ùïú : Type*}
    [Field ùïú] [LinearOrder ùïú] [IsStrictOrderedRing ùïú] [TopologicalSpace ùïú]
    [IsTopologicalRing ùïú] (a b c d : ùïú) (h : 0 < a) :
    affineHomeomorph a b h.ne' '' Set.Ioc c d = Set.Ioc (a * c + b) (a * d + b) := by
  simp [h]

theorem affineHomeomorph_image_Ioo {ùïú : Type*}
    [Field ùïú] [LinearOrder ùïú] [IsStrictOrderedRing ùïú] [TopologicalSpace ùïú]
    [IsTopologicalRing ùïú] (a b c d : ùïú) (h : 0 < a) :
    affineHomeomorph a b h.ne' '' Set.Ioo c d = Set.Ioo (a * c + b) (a * d + b) := by
  simp [h]

end affineHomeomorph

section LocalExtr

variable {Œ± Œ≤ : Type*} [TopologicalSpace Œ±]
  [Semifield Œ≤] [LinearOrder Œ≤] [IsStrictOrderedRing Œ≤] {a : Œ±}

open Topology

theorem IsLocalMin.inv {f : Œ± ‚Üí Œ≤} {a : Œ±} (h1 : IsLocalMin f a) (h2 : ‚àÄ·∂† z in ùìù a, 0 < f z) :
    IsLocalMax f‚Åª¬π a := by
  filter_upwards [h1, h2] with z h3 h4 using(inv_le_inv‚ÇÄ h4 h2.self_of_nhds).mpr h3

end LocalExtr

section Preconnected

/-! Some results about functions on preconnected sets valued in a ring or field with a topology. -/

open Set

variable {Œ± ùïú : Type*} {f g : Œ± ‚Üí ùïú} {S : Set Œ±} [TopologicalSpace Œ±] [TopologicalSpace ùïú]
  [T1Space ùïú]

/-- If `f` is a function `Œ± ‚Üí ùïú` which is continuous on a preconnected set `S`, and
`f ^ 2 = 1` on `S`, then either `f = 1` on `S`, or `f = -1` on `S`. -/
theorem IsPreconnected.eq_one_or_eq_neg_one_of_sq_eq [Ring ùïú] [NoZeroDivisors ùïú]
    (hS : IsPreconnected S) (hf : ContinuousOn f S) (hsq : EqOn (f ^ 2) 1 S) :
    EqOn f 1 S ‚à® EqOn f (-1) S := by
  have : DiscreteTopology ({1, -1} : Set ùïú) := Finite.instDiscreteTopology
  have hmaps : MapsTo f S {1, -1} := by
    simpa only [EqOn, Pi.one_apply, Pi.pow_apply, sq_eq_one_iff] using hsq
  simpa using hS.eqOn_const_of_mapsTo hf hmaps

/-- If `f, g` are functions `Œ± ‚Üí ùïú`, both continuous on a preconnected set `S`, with
`f ^ 2 = g ^ 2` on `S`, and `g z ‚â† 0` all `z ‚àà S`, then either `f = g` or `f = -g` on
`S`. -/
theorem IsPreconnected.eq_or_eq_neg_of_sq_eq [Field ùïú] [HasContinuousInv‚ÇÄ ùïú] [ContinuousMul ùïú]
    (hS : IsPreconnected S) (hf : ContinuousOn f S) (hg : ContinuousOn g S)
    (hsq : EqOn (f ^ 2) (g ^ 2) S) (hg_ne : ‚àÄ {x : Œ±}, x ‚àà S ‚Üí g x ‚â† 0) :
    EqOn f g S ‚à® EqOn f (-g) S := by
  have hsq : EqOn ((f / g) ^ 2) 1 S := fun x hx ‚Ü¶ by
    simpa [div_eq_one_iff_eq (pow_ne_zero _ (hg_ne hx)), div_pow] using hsq hx
  simpa +contextual [EqOn, div_eq_iff (hg_ne _)]
    using hS.eq_one_or_eq_neg_one_of_sq_eq (hf.div hg fun z ‚Ü¶ hg_ne) hsq

/-- If `f, g` are functions `Œ± ‚Üí ùïú`, both continuous on a preconnected set `S`, with
`f ^ 2 = g ^ 2` on `S`, and `g z ‚â† 0` all `z ‚àà S`, then as soon as `f = g` holds at
one point of `S` it holds for all points. -/
theorem IsPreconnected.eq_of_sq_eq [Field ùïú] [HasContinuousInv‚ÇÄ ùïú] [ContinuousMul ùïú]
    (hS : IsPreconnected S) (hf : ContinuousOn f S) (hg : ContinuousOn g S)
    (hsq : EqOn (f ^ 2) (g ^ 2) S) (hg_ne : ‚àÄ {x : Œ±}, x ‚àà S ‚Üí g x ‚â† 0) {y : Œ±} (hy : y ‚àà S)
    (hy' : f y = g y) : EqOn f g S := fun x hx ‚Ü¶ by
  rcases hS.eq_or_eq_neg_of_sq_eq hf hg @hsq @hg_ne with (h | h)
  ¬∑ exact h hx
  ¬∑ rw [h _, Pi.neg_apply, neg_eq_iff_add_eq_zero, ‚Üê two_mul, mul_eq_zero,
      (iff_of_eq (iff_false _)).2 (hg_ne _)] at hy' ‚ä¢ <;> assumption

end Preconnected

section ContinuousSMul

variable {F : Type*} [DivisionRing F] [TopologicalSpace F] [IsTopologicalRing F]
    (X : Type*) [TopologicalSpace X] [MulAction F X] [ContinuousSMul F X]

instance Subfield.continuousSMul (M : Subfield F) : ContinuousSMul M X :=
  Subring.continuousSMul M.toSubring X

end ContinuousSMul
