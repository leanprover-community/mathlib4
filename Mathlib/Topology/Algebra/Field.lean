/-
Copyright (c) 2021 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Kim Morrison
-/
import Mathlib.Algebra.Field.Subfield.Defs
import Mathlib.Algebra.GroupWithZero.Divisibility
import Mathlib.Algebra.Order.Group.Pointwise.Interval
import Mathlib.Topology.Algebra.GroupWithZero
import Mathlib.Topology.Algebra.Ring.Basic
import Mathlib.Topology.Order.LocalExtr

/-!
# Topological fields

A topological division ring is a topological ring whose inversion function is continuous at every
non-zero element.

-/

variable {K : Type*} [DivisionRing K] [TopologicalSpace K]

/-- Left-multiplication by a nonzero element of a topological division ring is proper, i.e.,
inverse images of compact sets are compact. -/
theorem Filter.tendsto_cocompact_mul_leftâ‚€ [ContinuousMul K] {a : K} (ha : a â‰  0) :
    Filter.Tendsto (fun x : K => a * x) (Filter.cocompact K) (Filter.cocompact K) :=
  Filter.tendsto_cocompact_mul_left (inv_mul_cancelâ‚€ ha)

/-- Right-multiplication by a nonzero element of a topological division ring is proper, i.e.,
inverse images of compact sets are compact. -/
theorem Filter.tendsto_cocompact_mul_rightâ‚€ [ContinuousMul K] {a : K} (ha : a â‰  0) :
    Filter.Tendsto (fun x : K => x * a) (Filter.cocompact K) (Filter.cocompact K) :=
  Filter.tendsto_cocompact_mul_right (mul_inv_cancelâ‚€ ha)

/-- Compact hausdorff topological fields are finite. -/
instance (priority := 100) {K} [DivisionRing K] [TopologicalSpace K]
    [IsTopologicalRing K] [CompactSpace K] [T2Space K] : Finite K := by
  suffices DiscreteTopology K by
    exact finite_of_compact_of_discrete
  rw [discreteTopology_iff_isOpen_singleton_zero]
  exact GroupWithZero.isOpen_singleton_zero

variable (K)

/-- A topological division ring is a division ring with a topology where all operations are
continuous, including inversion. -/
class IsTopologicalDivisionRing : Prop extends IsTopologicalRing K, HasContinuousInvâ‚€ K

@[deprecated (since := "2025-03-25")] alias TopologicalDivisionRing := IsTopologicalDivisionRing

section Subfield

variable {Î± : Type*} [Field Î±] [TopologicalSpace Î±] [IsTopologicalDivisionRing Î±]

/-- The (topological-space) closure of a subfield of a topological field is
itself a subfield. -/
def Subfield.topologicalClosure (K : Subfield Î±) : Subfield Î± :=
  { K.toSubring.topologicalClosure with
    carrier := _root_.closure (K : Set Î±)
    inv_mem' := fun x hx => by
      rcases eq_or_ne x 0 with (rfl | h)
      Â· rwa [inv_zero]
      Â· rw [â† inv_coe_set, â† Set.image_inv_eq_inv]
        exact mem_closure_image (continuousAt_invâ‚€ h) hx }

theorem Subfield.le_topologicalClosure (s : Subfield Î±) : s â‰¤ s.topologicalClosure :=
  _root_.subset_closure

theorem Subfield.isClosed_topologicalClosure (s : Subfield Î±) :
    IsClosed (s.topologicalClosure : Set Î±) :=
  isClosed_closure

theorem Subfield.topologicalClosure_minimal (s : Subfield Î±) {t : Subfield Î±} (h : s â‰¤ t)
    (ht : IsClosed (t : Set Î±)) : s.topologicalClosure â‰¤ t :=
  closure_minimal h ht

end Subfield

section affineHomeomorph

/-!
This section is about affine homeomorphisms from a topological field `ğ•œ` to itself.
Technically it does not require `ğ•œ` to be a topological field, a topological ring that
happens to be a field is enough.
-/


variable {ğ•œ : Type*} [Field ğ•œ] [TopologicalSpace ğ•œ] [IsTopologicalRing ğ•œ]

/--
The map `fun x => a * x + b`, as a homeomorphism from `ğ•œ` (a topological field) to itself,
when `a â‰  0`.
-/
@[simps]
def affineHomeomorph (a b : ğ•œ) (h : a â‰  0) : ğ•œ â‰ƒâ‚œ ğ•œ where
  toFun x := a * x + b
  invFun y := (y - b) / a
  left_inv x := by
    simp only [add_sub_cancel_right]
    exact mul_div_cancel_leftâ‚€ x h
  right_inv y := by simp [mul_div_cancelâ‚€ _ h]

theorem affineHomeomorph_image_Icc {ğ•œ : Type*}
    [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [TopologicalSpace ğ•œ]
    [IsTopologicalRing ğ•œ] (a b c d : ğ•œ) (h : 0 < a) :
    affineHomeomorph a b h.ne' '' Set.Icc c d = Set.Icc (a * c + b) (a * d + b) := by
  simp [h]

theorem affineHomeomorph_image_Ico {ğ•œ : Type*}
    [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [TopologicalSpace ğ•œ]
    [IsTopologicalRing ğ•œ] (a b c d : ğ•œ) (h : 0 < a) :
    affineHomeomorph a b h.ne' '' Set.Ico c d = Set.Ico (a * c + b) (a * d + b) := by
  simp [h]

theorem affineHomeomorph_image_Ioc {ğ•œ : Type*}
    [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [TopologicalSpace ğ•œ]
    [IsTopologicalRing ğ•œ] (a b c d : ğ•œ) (h : 0 < a) :
    affineHomeomorph a b h.ne' '' Set.Ioc c d = Set.Ioc (a * c + b) (a * d + b) := by
  simp [h]

theorem affineHomeomorph_image_Ioo {ğ•œ : Type*}
    [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [TopologicalSpace ğ•œ]
    [IsTopologicalRing ğ•œ] (a b c d : ğ•œ) (h : 0 < a) :
    affineHomeomorph a b h.ne' '' Set.Ioo c d = Set.Ioo (a * c + b) (a * d + b) := by
  simp [h]

end affineHomeomorph

section LocalExtr

variable {Î± Î² : Type*} [TopologicalSpace Î±]
  [Semifield Î²] [LinearOrder Î²] [IsStrictOrderedRing Î²] {a : Î±}

open Topology

theorem IsLocalMin.inv {f : Î± â†’ Î²} {a : Î±} (h1 : IsLocalMin f a) (h2 : âˆ€á¶  z in ğ“ a, 0 < f z) :
    IsLocalMax fâ»Â¹ a := by
  filter_upwards [h1, h2] with z h3 h4 using(inv_le_invâ‚€ h4 h2.self_of_nhds).mpr h3

end LocalExtr

section Preconnected

/-! Some results about functions on preconnected sets valued in a ring or field with a topology. -/

open Set

variable {Î± ğ•œ : Type*} {f g : Î± â†’ ğ•œ} {S : Set Î±} [TopologicalSpace Î±] [TopologicalSpace ğ•œ]
  [T1Space ğ•œ]

/-- If `f` is a function `Î± â†’ ğ•œ` which is continuous on a preconnected set `S`, and
`f ^ 2 = 1` on `S`, then either `f = 1` on `S`, or `f = -1` on `S`. -/
theorem IsPreconnected.eq_one_or_eq_neg_one_of_sq_eq [Ring ğ•œ] [NoZeroDivisors ğ•œ]
    (hS : IsPreconnected S) (hf : ContinuousOn f S) (hsq : EqOn (f ^ 2) 1 S) :
    EqOn f 1 S âˆ¨ EqOn f (-1) S := by
  have : DiscreteTopology ({1, -1} : Set ğ•œ) := Finite.instDiscreteTopology
  have hmaps : MapsTo f S {1, -1} := by
    simpa only [EqOn, Pi.one_apply, Pi.pow_apply, sq_eq_one_iff] using hsq
  simpa using hS.eqOn_const_of_mapsTo hf hmaps

/-- If `f, g` are functions `Î± â†’ ğ•œ`, both continuous on a preconnected set `S`, with
`f ^ 2 = g ^ 2` on `S`, and `g z â‰  0` all `z âˆˆ S`, then either `f = g` or `f = -g` on
`S`. -/
theorem IsPreconnected.eq_or_eq_neg_of_sq_eq [Field ğ•œ] [HasContinuousInvâ‚€ ğ•œ] [ContinuousMul ğ•œ]
    (hS : IsPreconnected S) (hf : ContinuousOn f S) (hg : ContinuousOn g S)
    (hsq : EqOn (f ^ 2) (g ^ 2) S) (hg_ne : âˆ€ {x : Î±}, x âˆˆ S â†’ g x â‰  0) :
    EqOn f g S âˆ¨ EqOn f (-g) S := by
  have hsq : EqOn ((f / g) ^ 2) 1 S := fun x hx => by
    simpa [div_eq_one_iff_eq (pow_ne_zero _ (hg_ne hx)), div_pow] using hsq hx
  simpa +contextual [EqOn, div_eq_iff (hg_ne _)]
    using hS.eq_one_or_eq_neg_one_of_sq_eq (hf.div hg fun z => hg_ne) hsq

/-- If `f, g` are functions `Î± â†’ ğ•œ`, both continuous on a preconnected set `S`, with
`f ^ 2 = g ^ 2` on `S`, and `g z â‰  0` all `z âˆˆ S`, then as soon as `f = g` holds at
one point of `S` it holds for all points. -/
theorem IsPreconnected.eq_of_sq_eq [Field ğ•œ] [HasContinuousInvâ‚€ ğ•œ] [ContinuousMul ğ•œ]
    (hS : IsPreconnected S) (hf : ContinuousOn f S) (hg : ContinuousOn g S)
    (hsq : EqOn (f ^ 2) (g ^ 2) S) (hg_ne : âˆ€ {x : Î±}, x âˆˆ S â†’ g x â‰  0) {y : Î±} (hy : y âˆˆ S)
    (hy' : f y = g y) : EqOn f g S := fun x hx => by
  rcases hS.eq_or_eq_neg_of_sq_eq hf hg @hsq @hg_ne with (h | h)
  Â· exact h hx
  Â· rw [h _, Pi.neg_apply, neg_eq_iff_add_eq_zero, â† two_mul, mul_eq_zero,
      (iff_of_eq (iff_false _)).2 (hg_ne _)] at hy' âŠ¢ <;> assumption

end Preconnected

section ContinuousSMul

variable {F : Type*} [DivisionRing F] [TopologicalSpace F] [IsTopologicalRing F]
    (X : Type*) [TopologicalSpace X] [MulAction F X] [ContinuousSMul F X]

instance Subfield.continuousSMul (M : Subfield F) : ContinuousSMul M X :=
  Subring.continuousSMul M.toSubring X

end ContinuousSMul
