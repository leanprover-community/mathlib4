/-
Copyright (c) 2025 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import Mathlib.Topology.Algebra.Group.Pointwise
import Mathlib.Topology.Algebra.Ring.Basic

/-!
# Restricted products of sets, groups and rings, and their topology

We define the **restricted product** of `R : Î¹ â†’ Type*` of types, relative to
a family of subsets `A : (i : Î¹) â†’ Set (R i)` and a filter `ğ“• : Filter Î¹`. This
is the set of all `x : Î  i, R i` such that the set `{j | x j âˆˆ A j}` belongs to `ğ“•`.
We denote it by `Î Ê³ i, [R i, A i]_[ğ“•]`.

The main case of interest, which we shall refer to as the "classical restricted product",
is that of `ğ“• = cofinite`. Recall that this is the filter of all subsets of `Î¹`, which are
*cofinite* in the sense that they have finite complement.
Hence, the associated restricted product is the set of all `x : Î  i, R i` such that
`x j âˆˆ A j` for all but finitely many `j`s. We denote it simply by `Î Ê³ i, [R i, A i]`.

Another notable case is that of the principal filter `ğ“• = ğ“Ÿ s` corresponding to some subset `s`
of `Î¹`. The associated restricted product `Î Ê³ i, [R i, A i]_[ğ“Ÿ s]` is the set of all
`x : Î  i, R i` such that `x j âˆˆ A j` for all `j âˆˆ s`. Put another way, this is just
`(Î  i âˆˆ s, A i) Ã— (Î  i âˆ‰ s, R i)`, modulo the obvious isomorphism.

We endow these types with the obvious algebraic structures, as well as their natural topology,
which we describe below. We also show various compatibility results.

In particular, with the theory of adeles in mind, we show that if each `R i` is a locally compact
topological ring with open subring `A i`, and if all but finitely many of the `A i`s are also
compact, then `Î Ê³ i, [R i, A i]` is a locally compact topological ring.

## Main definitions

* `RestrictedProduct`: the restricted product of a family `R` of types, relative to a family `A` of
  subsets and a filter `ğ“•` on the indexing set. This is denoted `Î Ê³ i, [R i, A i]_[ğ“•]`,
  or simply `Î Ê³ i, [R i, A i]` when `ğ“• = cofinite`.
* `RestrictedProduct.instDFunLike`: interpret an element of `Î Ê³ i, [R i, A i]_[ğ“•]` as an element
  of `Î  i, R i` using the `DFunLike` machinery.
* `RestrictedProduct.structureMap`: the inclusion map from `Î  i, A i` to `Î Ê³ i, [R i, A i]_[ğ“•]`.
* `RestrictedProduct.topologicalSpace`: the `TopologicalSpace` instance on `Î Ê³ i, [R i, A i]_[ğ“•]`.

## Topology on the restricted product

The topology on the restricted product `Î Ê³ i, [R i, A i]_[ğ“•]` is defined in the following way:
1. If `ğ“•` is some principal filter `ğ“Ÿ s`, recall that `Î Ê³ i, [R i, A i]_[ğ“Ÿ s]` is canonically
identified with `(Î  i âˆˆ s, A i) Ã— (Î  i âˆ‰ s, R i)`. We endow it with the product topology,
which is also the topology induced from the full product `Î  i, R i`.
2. In general, we note that `ğ“•` is the infimum of the principal filters coarser than `ğ“•`. We
then endow `Î Ê³ i, [R i, A i]_[ğ“•]` with the inductive limit / final topology associated to the
inclusion maps `Î Ê³ i, [R i, A i]_[ğ“Ÿ s] â†’ Î Ê³ i, [R i, A i]_[ğ“•]` where `ğ“• â‰¤ ğ“Ÿ s`.

In particular:
* On the classical restricted product, with respect to the cofinite filter, this corresponds to
  taking the inductive limit of the `Î Ê³ i, [R i, A i]_[ğ“Ÿ s]` over all *cofinite* sets `s : Set Î¹`.
* If `ğ“• = ğ“Ÿ s` is a principal filter, this second step clearly does not change the topology, since
  `s` belongs to the indexing set of the inductive limit.

Taking advantage of that second remark, we do not actually declare an instance specific to
principal filters. Instead, we provide directly the general instance (corresponding to step 2 above)
as `RestrictedProduct.topologicalSpace`. We then prove that, for a principal filter, the
map to the full product is an inducing (`RestrictedProduct.isEmbedding_coe_of_principal`),
and that the topology for a general `ğ“•` is indeed the expected inductive limit
(`RestrictedProduct.topologicalSpace_eq_iSup`).

## Main statements

* `RestrictedProduct.isEmbedding_coe_of_principal`: for any set `S`, `Î Ê³ i, [R i, A i]_[ğ“Ÿ S]`
  is endowed with the subset topology coming from `Î  i, R i`.
* `RestrictedProduct.topologicalSpace_eq_iSup`: the topology on `Î Ê³ i, [R i, A i]_[ğ“•]` is the
  inductive limit / final topology associated to the natural maps
  `Î Ê³ i, [R i, A i]_[ğ“Ÿ S] â†’ Î Ê³ i, [R i, A i]_[ğ“•]`, where `ğ“• â‰¤ ğ“Ÿ S`.
* `RestrictedProduct.continuous_dom`: a map from `Î Ê³ i, [R i, A i]_[ğ“•]` is continuous
*if and only if* its restriction to each `Î Ê³ i, [R i, A i]_[ğ“Ÿ s]` (with `ğ“• â‰¤ ğ“Ÿ s`) is continuous.
* `RestrictedProduct.continuous_dom_prod_left`: assume that each `A i` is an **open** subset of
`R i`. Then, for any topological space `Y`, a map from `Y Ã— Î Ê³ i, [R i, A i]` is continuous
*if and only if* its restriction to each `Y Ã— Î Ê³ i, [R i, A i]_[ğ“Ÿ S]` (with `S` cofinite)
is continuous.

* `RestrictedProduct.isTopologicalGroup`: if each `R i` is a topological group and each `A i` is an
  open subgroup of `R i`, then `Î Ê³ i, [R i, A i]` is a topological group.
* `RestrictedProduct.isTopologicalRing`: if each `R i` is a topological ring and each `A i` is an
  open subring of `R i`, then `Î Ê³ i, [R i, A i]` is a topological ring.
* `RestrictedProduct.continuousSMul`: if some topological monoid `G` acts on each `M i`, and each
  `A i` is stable for that action, then the natural action of `G` on `Î Ê³ i, [M i, A i]` is also
  continuous. In particular, if each `M i` is a topological `R`-module and each `A i` is an open
  sub-`R`-module of `M i`, then `Î Ê³ i, [M i, A i]` is a topological `R`-module.

* `RestrictedProduct.weaklyLocallyCompactSpace_of_cofinite`:  if each `R i` is weakly locally
  compact, each `A i` is open, and all but finitely many `A i`s are also compact, then the
  restricted product `Î Ê³ i, [R i, A i]` is weakly locally compact.
* `RestrictedProduct.locallyCompactSpace_of_group`: assume that each `R i` is a locally compact
  group with `A i` an open subgroup. Assume also that all but finitely many `A i`s are compact.
  Then the restricted product `Î Ê³ i, [R i, A i]` is a locally compact group.

## Notation

* `Î Ê³ i, [R i, A i]_[ğ“•]` is `RestrictedProduct R A ğ“•`.
* `Î Ê³ i, [R i, A i]` is `RestrictedProduct R A cofinite`.

## Implementation details

Outside of principal filters and the cofinite filter, the topology we define on the restricted
product does not seem well-behaved. While declaring a single instance is practical, it may conflict
with more interesting topologies in some other cases. Thus, future contributions should not
restrain from specializing these instances to principal and cofinite filters if necessary.

## Tags

restricted product, adeles, ideles
-/

open Set Topology Filter

variable {Î¹ : Type*}
variable (R : Î¹ â†’ Type*) (A : (i : Î¹) â†’ Set (R i))

/-!
## Definition and elementary maps
-/

/-- The **restricted product** of a family `R : Î¹ â†’ Type*` of types, relative to subsets
`A : (i : Î¹) â†’ Set (R i)` and the filter `ğ“• : Filter Î¹`, is the set of all `x : Î  i, R i`
such that the set `{j | x j âˆˆ A j}` belongs to `ğ“•`. We denote it by `Î Ê³ i, [R i, A i]_[ğ“•]`.

The most common use case is with `ğ“• = cofinite`, in which case the restricted product is the set
of all `x : Î  i, R i` such that `x j âˆˆ A j` for all but finitely many `j`. We denote it simply
by `Î Ê³ i, [R i, A i]`.

Similarly, if `S` is a principal filter, the restricted product `Î Ê³ i, [R i, A i]_[ğ“Ÿ s]`
is the set of all `x : Î  i, R i` such that `âˆ€ j âˆˆ S, x j âˆˆ A j`. -/
def RestrictedProduct (ğ“• : Filter Î¹) : Type _ := {x : Î  i, R i // âˆ€á¶  i in ğ“•, x i âˆˆ A i}

open Batteries.ExtendedBinder

/-- `Î Ê³ i, [R i, A i]_[ğ“•]` is `RestrictedProduct R A ğ“•`. -/
scoped[RestrictedProduct]
notation3 "Î Ê³ "(...)", ""["r:(scoped R => R)", "a:(scoped A => A)"]_[" f "]" =>
  RestrictedProduct r a f

/-- `Î Ê³ i, [R i, A i]` is `RestrictedProduct R A cofinite`. -/
scoped[RestrictedProduct]
notation3"Î Ê³ "(...)", ""["r:(scoped R => R)", "a:(scoped A => A)"]" =>
  RestrictedProduct r a cofinite

namespace RestrictedProduct

open scoped RestrictedProduct

variable {ğ“• ğ“– : Filter Î¹}

instance : DFunLike (Î Ê³ i, [R i, A i]_[ğ“•]) Î¹ R where
  coe x i := x.1 i
  coe_injective' _ _ := Subtype.ext

@[ext]
lemma ext {x y :  Î Ê³ i, [R i, A i]_[ğ“•]} (h : âˆ€ i, x i = y i) : x = y :=
  Subtype.ext <| funext h

lemma range_coe :
    range ((â†‘) : Î Ê³ i, [R i, A i]_[ğ“•] â†’ Î  i, R i) = {x | âˆ€á¶  i in ğ“•, x i âˆˆ A i} :=
  Subtype.range_val_subtype

lemma range_coe_principal {S : Set Î¹} :
    range ((â†‘) : Î Ê³ i, [R i, A i]_[ğ“Ÿ S] â†’ Î  i, R i) = S.pi A :=
  range_coe R A

@[simp] lemma eventually (x : Î Ê³ i, [R i, A i]_[ğ“•]) : âˆ€á¶  i in ğ“•, x i âˆˆ A i := x.2

variable (ğ“•) in
/-- The *structure map* of the restricted product is the obvious inclusion from `Î  i, A i`
into `Î Ê³ i, [R i, A i]_[ğ“•]`. -/
def structureMap (x : Î  i, A i) : Î Ê³ i, [R i, A i]_[ğ“•] :=
  âŸ¨fun i â†¦ x i, .of_forall fun i â†¦ (x i).2âŸ©

/-- If `ğ“• â‰¤ ğ“–`, the restricted product `Î Ê³ i, [R i, A i]_[ğ“–]` is naturally included in
`Î Ê³ i, [R i, A i]_[ğ“•]`. This is the corresponding map. -/
def inclusion (h : ğ“• â‰¤ ğ“–) (x : Î Ê³ i, [R i, A i]_[ğ“–]) :
    Î Ê³ i, [R i, A i]_[ğ“•] :=
  âŸ¨x, x.2.filter_mono hâŸ©

variable (ğ“•) in
lemma inclusion_eq_id : inclusion R A (le_refl ğ“•) = id := rfl

lemma exists_inclusion_eq_of_eventually (h : ğ“• â‰¤ ğ“–) {x : Î Ê³ i, [R i, A i]_[ğ“•]}
    (hxğ“– : âˆ€á¶  i in ğ“–, x i âˆˆ A i) :
    âˆƒ x' : Î Ê³ i, [R i, A i]_[ğ“–], inclusion R A h x' = x :=
  âŸ¨âŸ¨x.1, hxğ“–âŸ©, rflâŸ©

lemma exists_structureMap_eq_of_forall {x : Î Ê³ i, [R i, A i]_[ğ“•]}
    (hx : âˆ€ i, x.1 i âˆˆ A i) :
    âˆƒ x' : Î  i, A i, structureMap R A ğ“• x' = x :=
  âŸ¨fun i â†¦ âŸ¨x i, hx iâŸ©, rflâŸ©

lemma range_inclusion (h : ğ“• â‰¤ ğ“–) :
    Set.range (inclusion R A h) = {x | âˆ€á¶  i in ğ“–, x i âˆˆ A i} :=
  subset_antisymm (range_subset_iff.mpr fun x â†¦ x.2)
    (fun _ hx â†¦ mem_range.mpr <| exists_inclusion_eq_of_eventually R A h hx)

lemma range_structureMap :
    Set.range (structureMap R A ğ“•) = {f | âˆ€ i, f.1 i âˆˆ A i} :=
  subset_antisymm (range_subset_iff.mpr fun x i â†¦ (x i).2)
    (fun _ hx â†¦ mem_range.mpr <| exists_structureMap_eq_of_forall R A hx)

section Algebra
/-!
## Algebraic instances on restricted products

In this section, we endow the restricted product with its algebraic instances.
To avoid any unnecessary coercions, we use subobject classes for the subset `B i` of each `R i`.
-/

variable {S : Î¹ â†’ Type*} -- subobject type
variable [Î  i, SetLike (S i) (R i)]
variable {B : Î  i, S i}

@[to_additive]
instance [Î  i, One (R i)] [âˆ€ i, OneMemClass (S i) (R i)] : One (Î Ê³ i, [R i, B i]_[ğ“•]) where
  one := âŸ¨fun _ â†¦ 1, .of_forall fun _ â†¦ one_mem _âŸ©

@[to_additive]
instance [Î  i, Inv (R i)] [âˆ€ i, InvMemClass (S i) (R i)] : Inv (Î Ê³ i, [R i, B i]_[ğ“•]) where
  inv x := âŸ¨fun i â†¦ (x i)â»Â¹, x.2.mono fun _ â†¦ inv_memâŸ©

@[to_additive]
instance [Î  i, Mul (R i)] [âˆ€ i, MulMemClass (S i) (R i)] : Mul (Î Ê³ i, [R i, B i]_[ğ“•]) where
  mul x y := âŸ¨fun i â†¦ x i * y i, y.2.mp (x.2.mono fun _ â†¦ mul_mem)âŸ©

@[to_additive]
instance {G : Type*} [Î  i, SMul G (R i)] [âˆ€ i, SMulMemClass (S i) G (R i)] :
    SMul G (Î Ê³ i, [R i, B i]_[ğ“•]) where
  smul g x := âŸ¨fun i â†¦ g â€¢ (x i), x.2.mono fun _ â†¦ SMulMemClass.smul_mem gâŸ©

@[to_additive]
instance [Î  i, DivInvMonoid (R i)] [âˆ€ i, SubgroupClass (S i) (R i)] :
    Div (Î Ê³ i, [R i, B i]_[ğ“•]) where
  div x y := âŸ¨fun i â†¦ x i / y i, y.2.mp (x.2.mono fun _ â†¦ div_mem)âŸ©

instance [Î  i, Monoid (R i)] [âˆ€ i, SubmonoidClass (S i) (R i)] :
    Pow (Î Ê³ i, [R i, B i]_[ğ“•]) â„• where
  pow x n := âŸ¨fun i â†¦ x i ^ n, x.2.mono fun _ hi â†¦ pow_mem hi nâŸ©

instance [Î  i, AddMonoid (R i)] [âˆ€ i, AddSubmonoidClass (S i) (R i)] :
    AddMonoid (Î Ê³ i, [R i, B i]_[ğ“•]) :=
  haveI : âˆ€ i, SMulMemClass (S i) â„• (R i) := fun _ â†¦ AddSubmonoidClass.nsmulMemClass
  DFunLike.coe_injective.addMonoid _ rfl (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl)

@[to_additive existing]
instance [Î  i, Monoid (R i)] [âˆ€ i, SubmonoidClass (S i) (R i)] :
    Monoid (Î Ê³ i, [R i, B i]_[ğ“•]) :=
  DFunLike.coe_injective.monoid _ rfl (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl)

instance [Î  i, DivInvMonoid (R i)] [âˆ€ i, SubgroupClass (S i) (R i)] :
    Pow (Î Ê³ i, [R i, B i]_[ğ“•]) â„¤ where
  pow x n := âŸ¨fun i â†¦ x i ^ n, x.2.mono fun _ hi â†¦ zpow_mem hi nâŸ©

instance [Î  i, AddMonoidWithOne (R i)] [âˆ€ i, AddSubmonoidWithOneClass (S i) (R i)] :
    NatCast (Î Ê³ i, [R i, B i]_[ğ“•]) where
  natCast n := âŸ¨fun _ â†¦ n, .of_forall fun _ â†¦ natCast_mem _ nâŸ©

instance [Î  i, Ring (R i)] [âˆ€ i, SubringClass (S i) (R i)] :
    IntCast (Î Ê³ i, [R i, B i]_[ğ“•]) where
  intCast n := âŸ¨fun _ â†¦ n, .of_forall fun _ â†¦ intCast_mem _ nâŸ©

instance [Î  i, AddGroup (R i)] [âˆ€ i, AddSubgroupClass (S i) (R i)] :
    AddGroup (Î Ê³ i, [R i, B i]_[ğ“•]) :=
  haveI : âˆ€ i, SMulMemClass (S i) â„¤ (R i) := fun _ â†¦ AddSubgroupClass.zsmulMemClass
  haveI : âˆ€ i, SMulMemClass (S i) â„• (R i) := fun _ â†¦ AddSubmonoidClass.nsmulMemClass
  DFunLike.coe_injective.addGroup _ rfl (fun _ _ â†¦ rfl) (fun _ â†¦ rfl) (fun _ _ â†¦ rfl)
    (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl)

@[to_additive existing]
instance [Î  i, Group (R i)] [âˆ€ i, SubgroupClass (S i) (R i)] :
    Group (Î Ê³ i, [R i, B i]_[ğ“•]) :=
  DFunLike.coe_injective.group _ rfl (fun _ _ â†¦ rfl) (fun _ â†¦ rfl) (fun _ _ â†¦ rfl)
    (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl)

instance [Î  i, Ring (R i)] [âˆ€ i, SubringClass (S i) (R i)] :
    Ring (Î Ê³ i, [R i, B i]_[ğ“•]) :=
  DFunLike.coe_injective.ring _ rfl rfl (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl) (fun _ â†¦ rfl)
    (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl) (fun _ â†¦ rfl) (fun _ â†¦ rfl)

instance [Î  i, CommRing (R i)] [âˆ€ i, SubringClass (S i) (R i)] :
    CommRing (Î Ê³ i, [R i, B i]_[ğ“•]) where
  mul_comm _ _ := DFunLike.coe_injective <| funext (fun _ â†¦ mul_comm _ _)

end Algebra

section eval

variable {S : Î¹ â†’ Type*}
variable [Î  i, SetLike (S i) (R i)]
variable {B : Î  i, S i}

/-- `RestrictedProduct.evalMonoidHom j` is the monoid homomorphism from the restricted
product `Î Ê³ i, [R i, B i]_[ğ“•]` to the component `R j`.
-/
@[to_additive "`RestrictedProduct.evalAddMonoidHom j` is the monoid homomorphism from the restricted
product `Î Ê³ i, [R i, B i]_[ğ“•]` to the component `R j`."]
def evalMonoidHom (j : Î¹) [Î  i, Monoid (R i)] [âˆ€ i, SubmonoidClass (S i) (R i)] :
    (Î Ê³ i, [R i, B i]_[ğ“•]) â†’* R j where
  toFun x := x j
  map_one' := rfl
  map_mul' _ _ := rfl

@[simp]
lemma evalMonoidHom_apply [Î  i, Monoid (R i)] [âˆ€ i, SubmonoidClass (S i) (R i)]
    (x : Î Ê³ i, [R i, B i]_[ğ“•]) (j : Î¹) : evalMonoidHom R j x = x j :=
  rfl

/-- `RestrictedProduct.evalRingHom j` is the ring homomorphism from the restricted
product `Î Ê³ i, [R i, B i]_[ğ“•]` to the component `R j`.
-/
def evalRingHom (j : Î¹) [Î  i, Ring (R i)] [âˆ€ i, SubringClass (S i) (R i)] :
    (Î Ê³ i, [R i, B i]_[ğ“•]) â†’+* R j where
  __ := evalMonoidHom R j
  __ := evalAddMonoidHom R j

@[simp]
lemma evalRingHom_apply [Î  i, Ring (R i)] [âˆ€ i, SubringClass (S i) (R i)]
    (x : Î Ê³ i, [R i, B i]_[ğ“•]) (j : Î¹) : evalRingHom R j x = x j :=
  rfl

end eval

section map

variable {Î¹â‚ Î¹â‚‚ : Type*}
variable (Râ‚ : Î¹â‚ â†’ Type*) (Râ‚‚ : Î¹â‚‚ â†’ Type*)
variable {ğ“•â‚ : Filter Î¹â‚} {ğ“•â‚‚ : Filter Î¹â‚‚}
variable {Aâ‚ : (i : Î¹â‚) â†’ Set (Râ‚ i)} {Aâ‚‚ : (i : Î¹â‚‚) â†’ Set (Râ‚‚ i)}
variable {Sâ‚ : Î¹â‚ â†’ Type*} {Sâ‚‚ : Î¹â‚‚ â†’ Type*}
variable [Î  i, SetLike (Sâ‚ i) (Râ‚ i)] [Î  j, SetLike (Sâ‚‚ j) (Râ‚‚ j)]
variable {Bâ‚ : Î  i, Sâ‚ i} {Bâ‚‚ : Î  j, Sâ‚‚ j}
variable (f : Î¹â‚‚ â†’ Î¹â‚) (hf : Tendsto f ğ“•â‚‚ ğ“•â‚)

section set

variable (Ï† : âˆ€ j, Râ‚ (f j) â†’ Râ‚‚ j) (hÏ† : âˆ€á¶  j in ğ“•â‚‚, MapsTo (Ï† j) (Aâ‚ (f j)) (Aâ‚‚ j))

/--
Given two restricted products `Î Ê³ (i : Î¹â‚), [Râ‚ i, Aâ‚ i]_[ğ“•â‚]` and `Î Ê³ (j : Î¹â‚‚), [Râ‚‚ j, Aâ‚‚ j]_[ğ“•â‚‚]`,
`RestrictedProduct.map` gives a function between them. The data needed is a function `f : Î¹â‚‚ â†’ Î¹â‚`
such that `ğ“•â‚‚` tends to `ğ“•â‚` along `f`, and functions `Ï† j : Râ‚ (f j) â†’ Râ‚‚ j`
sending `Aâ‚ (f j)` into `Aâ‚‚ j` for an `ğ“•â‚‚`-large set of `j`'s.

See also `mapMonoidHom`, `mapAddMonoidHom` and `mapRingHom` for variants.
-/
def map (x : Î Ê³ i, [Râ‚ i, Aâ‚ i]_[ğ“•â‚]) : Î Ê³ j, [Râ‚‚ j, Aâ‚‚ j]_[ğ“•â‚‚] := âŸ¨fun j â†¦ Ï† j (x (f j)), by
  filter_upwards [hf.eventually x.2, hÏ†] using fun _ h1 h2 â†¦ h2 h1âŸ©

@[simp]
lemma map_apply (x : Î Ê³ i, [Râ‚ i, Aâ‚ i]_[ğ“•â‚]) (j : Î¹â‚‚) :
    x.map Râ‚ Râ‚‚ f hf Ï† hÏ† j = Ï† j (x (f j)) :=
  rfl

end set

section monoid

variable [Î  i, Monoid (Râ‚ i)] [Î  i, Monoid (Râ‚‚ i)] [âˆ€ i, SubmonoidClass (Sâ‚ i) (Râ‚ i)]
    [âˆ€ i, SubmonoidClass (Sâ‚‚ i) (Râ‚‚ i)] (Ï† : âˆ€ j, Râ‚ (f j) â†’* Râ‚‚ j)
    (hÏ† : âˆ€á¶  j in ğ“•â‚‚, MapsTo (Ï† j) (Bâ‚ (f j)) (Bâ‚‚ j))

/--
Given two restricted products `Î Ê³ (i : Î¹â‚), [Râ‚ i, Bâ‚ i]_[ğ“•â‚]` and `Î Ê³ (j : Î¹â‚‚), [Râ‚‚ j, Bâ‚‚ j]_[ğ“•â‚‚]`,
`RestrictedProduct.mapMonoidHom` gives a monoid homomorphism between them. The data needed is a
function `f : Î¹â‚‚ â†’ Î¹â‚` such that `ğ“•â‚‚` tends to `ğ“•â‚` along `f`, and monoid homomorphisms
`Ï† j : Râ‚ (f j) â†’ Râ‚‚ j` sending `Bâ‚ (f j)` into `Bâ‚‚ j` for an `ğ“•â‚‚`-large set of `j`'s.
-/
@[to_additive "
Given two restricted products `Î Ê³ (i : Î¹â‚), [Râ‚ i, Bâ‚ i]_[ğ“•â‚]` and `Î Ê³ (j : Î¹â‚‚), [Râ‚‚ j, Bâ‚‚ j]_[ğ“•â‚‚]`,
`RestrictedProduct.mapAddMonoidHom` gives a additive monoid homomorphism between them. The data
needed is a function `f : Î¹â‚‚ â†’ Î¹â‚` such that `ğ“•â‚‚` tends to `ğ“•â‚` along `f`, and
additive monoid homomorphisms `Ï† j : Râ‚ (f j) â†’ Râ‚‚ j` sending `Bâ‚ (f j)` into `Bâ‚‚ j` for
an `ğ“•â‚‚`-large set of `j`'s.
"]
def mapMonoidHom : Î Ê³ i, [Râ‚ i, Bâ‚ i]_[ğ“•â‚] â†’* Î Ê³ j, [Râ‚‚ j, Bâ‚‚ j]_[ğ“•â‚‚] where
  toFun := map Râ‚ Râ‚‚ f hf (fun j r â†¦ Ï† j r) hÏ†
  map_one' := by
    ext i
    exact map_one (Ï† i)
  map_mul' x y := by
    ext i
    exact map_mul (Ï† i) _ _

@[to_additive (attr := simp)]
lemma mapMonoidHom_apply (x : Î Ê³ i, [Râ‚ i, Bâ‚ i]_[ğ“•â‚]) (j : Î¹â‚‚) :
    x.mapMonoidHom Râ‚ Râ‚‚ f hf Ï† hÏ† j = Ï† j (x (f j)) :=
  rfl

end monoid

section ring

variable [Î  i, Ring (Râ‚ i)] [Î  i, Ring (Râ‚‚ i)] [âˆ€ i, SubringClass (Sâ‚ i) (Râ‚ i)]
    [âˆ€ i, SubringClass (Sâ‚‚ i) (Râ‚‚ i)] (Ï† : âˆ€ j, Râ‚ (f j) â†’+* Râ‚‚ j)
    (hÏ† : âˆ€á¶  j in ğ“•â‚‚, MapsTo (Ï† j) (Bâ‚ (f j)) (Bâ‚‚ j))

/--
Given two restricted products `Î Ê³ (i : Î¹â‚), [Râ‚ i, Bâ‚ i]_[ğ“•â‚]` and `Î Ê³ (j : Î¹â‚‚), [Râ‚‚ j, Bâ‚‚ j]_[ğ“•â‚‚]`,
`RestrictedProduct.mapRingHom` gives a ring homomorphism between them. The data needed is a
function `f : Î¹â‚‚ â†’ Î¹â‚` such that `ğ“•â‚‚` tends to `ğ“•â‚` along `f`, and ring homomorphisms
`Ï† j : Râ‚ (f j) â†’ Râ‚‚ j` sending `Bâ‚ (f j)` into `Bâ‚‚ j` for an `ğ“•â‚‚`-large set of `j`'s.
-/
def mapRingHom : Î Ê³ i, [Râ‚ i, Bâ‚ i]_[ğ“•â‚] â†’+* Î Ê³ j, [Râ‚‚ j, Bâ‚‚ j]_[ğ“•â‚‚] where
  __ := mapMonoidHom Râ‚ Râ‚‚ f hf (fun j â†¦ Ï† j) hÏ†
  __ := mapAddMonoidHom Râ‚ Râ‚‚ f hf (fun j â†¦ Ï† j) hÏ†

@[simp]
lemma mapRingHom_apply (x : Î Ê³ i, [Râ‚ i, Bâ‚ i]_[ğ“•â‚]) (j : Î¹â‚‚) :
    x.mapRingHom Râ‚ Râ‚‚ f hf Ï† hÏ† j = Ï† j (x (f j)) :=
  rfl

end ring

end map

section Topology
/-!
## Topology on the restricted product

The topology on the restricted product `Î Ê³ i, [R i, A i]_[ğ“•]` is defined in the following way:
1. If `ğ“•` is some principal filter `ğ“Ÿ s`, recall that `Î Ê³ i, [R i, A i]_[ğ“Ÿ s]` is canonically
identified with `(Î  i âˆˆ s, A i) Ã— (Î  i âˆ‰ s, R i)`. We endow it with the product topology,
which is also the topology induced from the full product `Î  i, R i`.
2. In general, we note that `ğ“•` is the infimum of the principal filters coarser than `ğ“•`. We
then endow `Î Ê³ i, [R i, A i]_[ğ“•]` with the inductive limit / final topology associated to the
inclusion maps `Î Ê³ i, [R i, A i]_[ğ“Ÿ s] â†’ Î Ê³ i, [R i, A i]_[ğ“•]` where `ğ“• â‰¤ ğ“Ÿ s`.

In particular:
* On the classical restricted product, with respect to the cofinite filter, this corresponds to
  taking the inductive limit of the `Î Ê³ i, [R i, A i]_[ğ“Ÿ s]` over all *cofinite* sets `s : Set Î¹`.
* If `ğ“• = ğ“Ÿ s` is a principal filter, this second step clearly does not change the topology, since
  `s` belongs to the indexing set of the inductive limit.

Taking advantage of that second remark, we do not actually declare an instance specific to
principal filters. Instead, we provide directly the general instance (corresponding to step 2 above)
as `RestrictedProduct.topologicalSpace`. We then prove that, for a principal filter, the
map to the full product is an inducing (`RestrictedProduct.isEmbedding_coe_of_principal`),
and that the topology for a general `ğ“•` is indeed the expected inductive limit
(`RestrictedProduct.topologicalSpace_eq_iSup`).

Note: outside of these two cases, this topology on the restricted product does not seem
well-behaved. While declaring a single instance is practical, it may conflict with more interesting
topologies in some other cases. Thus, future contributions should not restrain from specializing
these instances to principal and cofinite filters if necessary.
-/

/-!
### Definition of the topology
-/

variable {R A R' A'}
variable {ğ“• : Filter Î¹}
variable [âˆ€ i, TopologicalSpace (R i)]

variable (R A ğ“•) in
instance topologicalSpace : TopologicalSpace (Î Ê³ i, [R i, A i]_[ğ“•]) :=
  â¨† (S : Set Î¹) (hS : ğ“• â‰¤ ğ“Ÿ S), .coinduced (inclusion R A hS)
    (.induced ((â†‘) : Î Ê³ i, [R i, A i]_[ğ“Ÿ S] â†’ Î  i, R i) inferInstance)

@[fun_prop]
theorem continuous_coe :
    Continuous ((â†‘) : Î Ê³ i, [R i, A i]_[ğ“•] â†’ Î  i, R i) :=
  continuous_iSup_dom.mpr fun _ â†¦ continuous_iSup_dom.mpr fun _ â†¦
    continuous_coinduced_dom.mpr continuous_induced_dom

@[fun_prop]
theorem continuous_eval (i : Î¹) :
    Continuous (fun (x : Î Ê³ i, [R i, A i]_[ğ“•]) â†¦ x i) :=
  continuous_apply _ |>.comp continuous_coe

@[fun_prop]
theorem continuous_inclusion {ğ“– : Filter Î¹} (h : ğ“• â‰¤ ğ“–) :
    Continuous (inclusion R A h) := by
  simp_rw [continuous_iff_coinduced_le, topologicalSpace, coinduced_iSup, coinduced_compose]
  exact iSupâ‚‚_le fun S hS â†¦ le_iSupâ‚‚_of_le S (le_trans h hS) le_rfl

instance [âˆ€ i, T0Space (R i)] : T0Space (Î Ê³ i, [R i, A i]_[ğ“•]) :=
  t0Space_of_injective_of_continuous DFunLike.coe_injective continuous_coe

instance [âˆ€ i, T1Space (R i)] : T1Space (Î Ê³ i, [R i, A i]_[ğ“•]) :=
  t1Space_of_injective_of_continuous DFunLike.coe_injective continuous_coe

instance [âˆ€ i, T2Space (R i)] : T2Space (Î Ê³ i, [R i, A i]_[ğ“•]) :=
  .of_injective_continuous DFunLike.coe_injective continuous_coe

section principal
/-!
### Topological facts in the principal case
-/

variable {S : Set Î¹}

theorem topologicalSpace_eq_of_principal :
    topologicalSpace R A (ğ“Ÿ S) =
      .induced ((â†‘) : Î Ê³ i, [R i, A i]_[ğ“Ÿ S] â†’ Î  i, R i) inferInstance :=
  le_antisymm (continuous_iff_le_induced.mp continuous_coe) <|
    (le_iSupâ‚‚_of_le S le_rfl <| by rw [inclusion_eq_id R A (ğ“Ÿ S), @coinduced_id])

theorem topologicalSpace_eq_of_top :
    topologicalSpace R A âŠ¤ =
      .induced ((â†‘) : Î Ê³ i, [R i, A i]_[âŠ¤] â†’ Î  i, R i) inferInstance :=
  principal_univ â–¸ topologicalSpace_eq_of_principal

theorem topologicalSpace_eq_of_bot :
    topologicalSpace R A âŠ¥ =
      .induced ((â†‘) : Î Ê³ i, [R i, A i]_[âŠ¥] â†’ Î  i, R i) inferInstance :=
  principal_empty â–¸ topologicalSpace_eq_of_principal

theorem isEmbedding_coe_of_principal :
    IsEmbedding ((â†‘) : Î Ê³ i, [R i, A i]_[ğ“Ÿ S] â†’ Î  i, R i) where
  eq_induced := topologicalSpace_eq_of_principal
  injective := DFunLike.coe_injective

theorem isEmbedding_coe_of_top :
    IsEmbedding ((â†‘) : Î Ê³ i, [R i, A i]_[âŠ¤] â†’ Î  i, R i) :=
  principal_univ â–¸ isEmbedding_coe_of_principal

theorem isEmbedding_coe_of_bot :
    IsEmbedding ((â†‘) : Î Ê³ i, [R i, A i]_[âŠ¥] â†’ Î  i, R i) :=
  principal_empty â–¸ isEmbedding_coe_of_principal

theorem continuous_rng_of_principal {X : Type*} [TopologicalSpace X]
    {f : X â†’ Î Ê³ i, [R i, A i]_[ğ“Ÿ S]} :
    Continuous f â†” Continuous ((â†‘) âˆ˜ f : X â†’ Î  i, R i) :=
  isEmbedding_coe_of_principal.continuous_iff

theorem continuous_rng_of_top {X : Type*} [TopologicalSpace X]
    {f : X â†’ Î Ê³ i, [R i, A i]_[âŠ¤]} :
    Continuous f â†” Continuous ((â†‘) âˆ˜ f : X â†’ Î  i, R i) :=
  isEmbedding_coe_of_top.continuous_iff

theorem continuous_rng_of_bot {X : Type*} [TopologicalSpace X]
    {f : X â†’ Î Ê³ i, [R i, A i]_[âŠ¥]} :
    Continuous f â†” Continuous ((â†‘) âˆ˜ f : X â†’ Î  i, R i) :=
  isEmbedding_coe_of_bot.continuous_iff

lemma continuous_rng_of_principal_iff_forall {X : Type*} [TopologicalSpace X]
    {f : X â†’ Î Ê³ (i : Î¹), [R i, A i]_[ğ“Ÿ S]} :
    Continuous f â†” âˆ€ i : Î¹, Continuous ((fun x â†¦ x i) âˆ˜ f) :=
  continuous_rng_of_principal.trans continuous_pi_iff

/-- The obvious bijection between `Î Ê³ i, [R i, A i]_[âŠ¤]` and `Î  i, A i` is a homeomorphism. -/
def homeoTop : (Î  i, A i) â‰ƒâ‚œ (Î Ê³ i, [R i, A i]_[âŠ¤]) where
  toFun f := âŸ¨fun i â†¦ f i, fun i â†¦ (f i).2âŸ©
  invFun f i := âŸ¨f i, f.2 iâŸ©
  continuous_toFun := continuous_rng_of_top.mpr <| continuous_pi fun i â†¦
    continuous_subtype_val.comp <| continuous_apply i
  continuous_invFun := continuous_pi fun i â†¦ continuous_induced_rng.mpr <| continuous_eval i
  left_inv _ := rfl
  right_inv _ := rfl

/-- The obvious bijection between `Î Ê³ i, [R i, A i]_[âŠ¥]` and `Î  i, R i` is a homeomorphism. -/
def homeoBot : (Î  i, R i) â‰ƒâ‚œ (Î Ê³ i, [R i, A i]_[âŠ¥]) where
  toFun f := âŸ¨fun i â†¦ f i, eventually_botâŸ©
  invFun f i := f i
  continuous_toFun := continuous_rng_of_bot.mpr <| continuous_pi fun i â†¦ continuous_apply i
  continuous_invFun := continuous_pi continuous_eval
  left_inv _ := rfl
  right_inv _ := rfl

/-- Assume that `S` is a subset of `Î¹` with finite complement, that each `R i` is weakly locally
compact, and that `A i` is *compact* for all `i âˆˆ S`. Then the restricted product
`Î Ê³ i, [R i, A i]_[ğ“Ÿ S]` is locally compact.

Note: we spell "`S` has finite complement" as `cofinite â‰¤ ğ“Ÿ S`. -/
theorem weaklyLocallyCompactSpace_of_principal [âˆ€ i, WeaklyLocallyCompactSpace (R i)]
    (hS : cofinite â‰¤ ğ“Ÿ S) (hAcompact : âˆ€ i âˆˆ S, IsCompact (A i)) :
    WeaklyLocallyCompactSpace (Î Ê³ i, [R i, A i]_[ğ“Ÿ S]) where
  exists_compact_mem_nhds := fun x â†¦ by
    rw [le_principal_iff, mem_cofinite] at hS
    classical
    have : âˆ€ i, âˆƒ K, IsCompact K âˆ§ K âˆˆ ğ“ (x i) := fun i â†¦ exists_compact_mem_nhds (x i)
    choose K K_compact hK using this
    set Q : Set (Î  i, R i) := univ.pi (fun i â†¦ if i âˆˆ S then A i else K i) with Q_def
    have Q_compact : IsCompact Q := isCompact_univ_pi fun i â†¦ by
      split_ifs with his
      Â· exact hAcompact i his
      Â· exact K_compact i
    set U : Set (Î  i, R i) := Sá¶œ.pi K
    have U_nhds : U âˆˆ ğ“ (x : Î  i, R i) := set_pi_mem_nhds hS fun i _ â†¦ hK i
    have QU : (â†‘) â»Â¹' U âŠ† ((â†‘) â»Â¹' Q : Set (Î Ê³ i, [R i, A i]_[ğ“Ÿ S])) := fun y H i _ â†¦ by
      dsimp only
      split_ifs with hi
      Â· exact y.2 hi
      Â· exact H i hi
    refine âŸ¨((â†‘) â»Â¹' Q), ?_, mem_of_superset ?_ QUâŸ©
    Â· refine isEmbedding_coe_of_principal.isCompact_preimage_iff ?_ |>.mpr Q_compact
      simp_rw [range_coe_principal, Q_def, pi_if, mem_univ, true_and]
      exact inter_subset_left
    Â· simpa only [isEmbedding_coe_of_principal.nhds_eq_comap] using preimage_mem_comap U_nhds

instance [âˆ€ i, WeaklyLocallyCompactSpace (R i)] [hS : Fact (cofinite â‰¤ ğ“Ÿ S)]
    [hAcompact : âˆ€ i, CompactSpace (A i)] :
    WeaklyLocallyCompactSpace (Î Ê³ i, [R i, A i]_[ğ“Ÿ S]) :=
  weaklyLocallyCompactSpace_of_principal hS.out
    fun _ _ â†¦ isCompact_iff_compactSpace.mpr inferInstance

end principal

section general
/-!
### Topological facts in the general case
-/

variable (ğ“•) in
theorem topologicalSpace_eq_iSup :
    topologicalSpace R A ğ“• = â¨† (S : Set Î¹) (hS : ğ“• â‰¤ ğ“Ÿ S),
      .coinduced (inclusion R A hS) (topologicalSpace R A (ğ“Ÿ S)) := by
  simp_rw [topologicalSpace_eq_of_principal, topologicalSpace]

/-- The **universal property** of the topology on the restricted product: a map from
`Î Ê³ i, [R i, A i]_[ğ“•]` is continuous *iff* its restriction to each `Î Ê³ i, [R i, A i]_[ğ“Ÿ s]`
(with `ğ“• â‰¤ ğ“Ÿ s`) is continuous.

See also `RestrictedProduct.continuous_dom_prod_left`. -/
theorem continuous_dom {X : Type*} [TopologicalSpace X]
    {f : Î Ê³ i, [R i, A i]_[ğ“•] â†’ X} :
    Continuous f â†” âˆ€ (S : Set Î¹) (hS : ğ“• â‰¤ ğ“Ÿ S), Continuous (f âˆ˜ inclusion R A hS) := by
  simp_rw [topologicalSpace_eq_of_principal, continuous_iSup_dom, continuous_coinduced_dom]

theorem isEmbedding_inclusion_principal {S : Set Î¹} (hS : ğ“• â‰¤ ğ“Ÿ S) :
    IsEmbedding (inclusion R A hS) :=
  .of_comp (continuous_inclusion hS) continuous_coe isEmbedding_coe_of_principal

theorem isEmbedding_inclusion_top :
    IsEmbedding (inclusion R A (le_top : ğ“• â‰¤ âŠ¤)) :=
  .of_comp (continuous_inclusion _) continuous_coe isEmbedding_coe_of_top

/-- `Î  i, A i` has the subset topology from the restricted product. -/
theorem isEmbedding_structureMap :
    IsEmbedding (structureMap R A ğ“•) :=
  isEmbedding_inclusion_top.comp homeoTop.isEmbedding

end general

section cofinite
/-!
### Topological facts in the case where `ğ“• = cofinite` and all `A i`s are open

The classical restricted product, associated to the cofinite filter, satisfies more topological
properties when each `A i` is an open subset of `R i`. The key fact is that each
`Î Ê³ i, [R i, A i]_[ğ“Ÿ S]` (with `S` cofinite) then embeds **as an open subset** in
`Î Ê³ i, [R i, A i]`.

This allows us to prove a "universal property with parameters", expressing that for any
arbitrary topolgical space `X` (of "parameters"), the product `X Ã— Î Ê³ i, [R i, A i]`
is still the inductive limit of the `X Ã— Î Ê³ i, [R i, A i]_[ğ“Ÿ S]` for `S` cofinite.

This fact, which is **not true** for a general inductive limit, will allow us to prove continuity
of functions of two variables (e.g algebraic operations), which would otherwise be inaccessible.
-/

variable (hAopen : âˆ€ i, IsOpen (A i))

include hAopen in
theorem isOpen_forall_imp_mem_of_principal {S : Set Î¹} (hS : cofinite â‰¤ ğ“Ÿ S) {p : Î¹ â†’ Prop} :
    IsOpen {f : Î Ê³ i, [R i, A i]_[ğ“Ÿ S] | âˆ€ i, p i â†’ f.1 i âˆˆ A i} := by
  rw [le_principal_iff] at hS
  convert isOpen_set_pi (hS.inter_of_left {i | p i}) (fun i _ â†¦ hAopen i) |>.preimage continuous_coe
  ext f
  refine âŸ¨fun H i hi â†¦ H i hi.2, fun H i hiT â†¦ ?_âŸ©
  by_cases hiS : i âˆˆ S
  Â· exact f.2 hiS
  Â· exact H i âŸ¨hiS, hiTâŸ©

include hAopen in
theorem isOpen_forall_mem_of_principal {S : Set Î¹} (hS : cofinite â‰¤ ğ“Ÿ S) :
    IsOpen {f : Î Ê³ i, [R i, A i]_[ğ“Ÿ S] | âˆ€ i, f.1 i âˆˆ A i} := by
  convert isOpen_forall_imp_mem_of_principal hAopen hS (p := fun _ â†¦ True)
  simp

include hAopen in
theorem isOpen_forall_imp_mem {p : Î¹ â†’ Prop} :
    IsOpen {f : Î Ê³ i, [R i, A i] | âˆ€ i, p i â†’ f.1 i âˆˆ A i} := by
  simp_rw [topologicalSpace_eq_iSup cofinite, isOpen_iSup_iff, isOpen_coinduced]
  exact fun S hS â†¦ isOpen_forall_imp_mem_of_principal hAopen hS

include hAopen in
theorem isOpen_forall_mem :
    IsOpen {f : Î Ê³ i, [R i, A i] | âˆ€ i, f.1 i âˆˆ A i} := by
  simp_rw [topologicalSpace_eq_iSup cofinite, isOpen_iSup_iff, isOpen_coinduced]
  exact fun S hS â†¦ isOpen_forall_mem_of_principal hAopen hS

include hAopen in
theorem isOpenEmbedding_inclusion_principal {S : Set Î¹} (hS : cofinite â‰¤ ğ“Ÿ S) :
    IsOpenEmbedding (inclusion R A hS) where
  toIsEmbedding := isEmbedding_inclusion_principal hS
  isOpen_range := by
    rw [range_inclusion]
    exact isOpen_forall_imp_mem hAopen

include hAopen in
/-- `Î  i, A i` is homeomorphic to an open subset of the restricted product. -/
theorem isOpenEmbedding_structureMap :
    IsOpenEmbedding (structureMap R A cofinite) where
  toIsEmbedding := isEmbedding_structureMap
  isOpen_range := by
    rw [range_structureMap]
    exact isOpen_forall_mem hAopen

include hAopen in
theorem nhds_eq_map_inclusion {S : Set Î¹} (hS : cofinite â‰¤ ğ“Ÿ S)
    (x : Î Ê³ i, [R i, A i]_[ğ“Ÿ S]) :
    (ğ“ (inclusion R A hS x)) = .map (inclusion R A hS) (ğ“ x) := by
  rw [isOpenEmbedding_inclusion_principal hAopen hS |>.map_nhds_eq x]

include hAopen in
theorem nhds_eq_map_structureMap
    (x : Î  i, A i) :
    (ğ“ (structureMap R A cofinite x)) = .map (structureMap R A cofinite) (ğ“ x) := by
  rw [isOpenEmbedding_structureMap hAopen |>.map_nhds_eq x]

include hAopen in
/-- If each `R i` is weakly locally compact, each `A i` is open, and all but finitely many `A i`s
are also compact, then the restricted product `Î Ê³ i, [R i, A i]` is weakly locally compact. -/
theorem weaklyLocallyCompactSpace_of_cofinite [âˆ€ i, WeaklyLocallyCompactSpace (R i)]
    (hAcompact : âˆ€á¶  i in cofinite, IsCompact (A i)) :
    WeaklyLocallyCompactSpace (Î Ê³ i, [R i, A i]) where
  exists_compact_mem_nhds := fun x â†¦ by
    set S := {i | IsCompact (A i) âˆ§ x i âˆˆ A i}
    have hS : cofinite â‰¤ ğ“Ÿ S := le_principal_iff.mpr (hAcompact.and x.2)
    have hSx : âˆ€ i âˆˆ S, x i âˆˆ A i := fun i hi â†¦ hi.2
    have hSA : âˆ€ i âˆˆ S, IsCompact (A i) := fun i hi â†¦ hi.1
    haveI := weaklyLocallyCompactSpace_of_principal hS hSA
    rcases exists_inclusion_eq_of_eventually R A hS hSx with âŸ¨x', hxx'âŸ©
    rw [â† hxx', nhds_eq_map_inclusion hAopen]
    rcases exists_compact_mem_nhds x' with âŸ¨K, K_compact, hKâŸ©
    exact âŸ¨inclusion R A hS '' K, K_compact.image (continuous_inclusion hS), image_mem_map hKâŸ©

instance [hAopen : Fact (âˆ€ i, IsOpen (A i))] [âˆ€ i, WeaklyLocallyCompactSpace (R i)]
    [hAcompact : âˆ€ i, CompactSpace (A i)] :
    WeaklyLocallyCompactSpace (Î Ê³ i, [R i, A i]) :=
  weaklyLocallyCompactSpace_of_cofinite hAopen.out <|
    .of_forall fun _ â†¦ isCompact_iff_compactSpace.mpr inferInstance

include hAopen in
/-- The **universal property with parameters** of the topology on the restricted product:
for any topological space `Y` of "parameters", a map from `(Î Ê³ i, [R i, A i]) Ã— Y` is continuous
*iff* its restriction to each `(Î Ê³ i, [R i, A i]_[ğ“Ÿ S]) Ã— Y` (with `S` cofinite) is continuous. -/
theorem continuous_dom_prod_right {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    {f : Î Ê³ i, [R i, A i] Ã— Y â†’ X} :
    Continuous f â†” âˆ€ (S : Set Î¹) (hS : cofinite â‰¤ ğ“Ÿ S),
      Continuous (f âˆ˜ (Prod.map (inclusion R A hS) id)) := by
  refine âŸ¨fun H S hS â†¦ H.comp ((continuous_inclusion hS).prodMap continuous_id),
    fun H â†¦ ?_âŸ©
  simp_rw [continuous_iff_continuousAt, ContinuousAt]
  rintro âŸ¨x, yâŸ©
  set S : Set Î¹ := {i | x i âˆˆ A i}
  have hS : cofinite â‰¤ ğ“Ÿ S := le_principal_iff.mpr x.2
  have hxS : âˆ€ i âˆˆ S, x i âˆˆ A i := fun i hi â†¦ hi
  rcases exists_inclusion_eq_of_eventually R A hS hxS with âŸ¨x', hxx'âŸ©
  rw [â† hxx', nhds_prod_eq, nhds_eq_map_inclusion hAopen hS x',
    â† Filter.map_id (f := ğ“ y), prod_map_map_eq, â† nhds_prod_eq, tendsto_map'_iff]
  exact H S hS |>.tendsto âŸ¨x', yâŸ©

-- TODO: get from the previous one instead of copy-pasting
include hAopen in
/-- The **universal property with parameters** of the topology on the restricted product:
for any topological space `Y` of "parameters", a map from `Y Ã— Î Ê³ i, [R i, A i]` is continuous
*iff* its restriction to each `Y Ã— Î Ê³ i, [R i, A i]_[ğ“Ÿ S]` (with `S` cofinite) is continuous. -/
theorem continuous_dom_prod_left {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    {f : Y Ã— Î Ê³ i, [R i, A i] â†’ X} :
    Continuous f â†” âˆ€ (S : Set Î¹) (hS : cofinite â‰¤ ğ“Ÿ S),
      Continuous (f âˆ˜ (Prod.map id (inclusion R A hS))) := by
  refine âŸ¨fun H S hS â†¦ H.comp (continuous_id.prodMap (continuous_inclusion hS)),
    fun H â†¦ ?_âŸ©
  simp_rw [continuous_iff_continuousAt, ContinuousAt]
  rintro âŸ¨y, xâŸ©
  set S : Set Î¹ := {i | x i âˆˆ A i}
  have hS : cofinite â‰¤ ğ“Ÿ S := le_principal_iff.mpr x.2
  have hxS : âˆ€ i âˆˆ S, x i âˆˆ A i := fun i hi â†¦ hi
  rcases exists_inclusion_eq_of_eventually R A hS hxS with âŸ¨x', hxx'âŸ©
  rw [â† hxx', nhds_prod_eq, nhds_eq_map_inclusion hAopen hS x',
    â† Filter.map_id (f := ğ“ y), prod_map_map_eq, â† nhds_prod_eq, tendsto_map'_iff]
  exact H S hS |>.tendsto âŸ¨y, x'âŸ©

include hAopen in
/-- A map from `Î Ê³ i, [R i, A i] Ã— Î Ê³ i, [R' i, A' i]` is continuous
*iff* its restriction to each `Î Ê³ i, [R i, A i]_[ğ“Ÿ S] Ã— Î Ê³ i, [R' i, A' i]_[ğ“Ÿ S]`
(with `S` cofinite) is continuous.

This is the key result for continuity of multiplication and addition. -/
theorem continuous_dom_prod {R' : Î¹ â†’ Type*} {A' : (i : Î¹) â†’ Set (R' i)}
    [âˆ€ i, TopologicalSpace (R' i)] (hAopen' : âˆ€ i, IsOpen (A' i))
    {X : Type*} [TopologicalSpace X]
    {f : Î Ê³ i, [R i, A i] Ã— Î Ê³ i, [R' i, A' i] â†’ X} :
    Continuous f â†” âˆ€ (S : Set Î¹) (hS : cofinite â‰¤ ğ“Ÿ S),
      Continuous (f âˆ˜ (Prod.map (inclusion R A hS) (inclusion R' A' hS))) := by
  simp_rw [continuous_dom_prod_right hAopen, continuous_dom_prod_left hAopen']
  refine âŸ¨fun H S hS â†¦ H S hS S hS, fun H S hS T hT â†¦ ?_âŸ©
  set U := S âˆ© T
  have hU : cofinite â‰¤ ğ“Ÿ (S âˆ© T) := inf_principal â–¸ le_inf hS hT
  have hSU : ğ“Ÿ U â‰¤ ğ“Ÿ S := principal_mono.mpr inter_subset_left
  have hTU : ğ“Ÿ U â‰¤ ğ“Ÿ T := principal_mono.mpr inter_subset_right
  exact (H U hU).comp ((continuous_inclusion hSU).prodMap (continuous_inclusion hTU))

/-- A finitary (instead of binary) version of `continuous_dom_prod`. -/
theorem continuous_dom_pi {n : Type*} [Fintype n] {X : Type*}
    [TopologicalSpace X] {A : n â†’ Î¹ â†’ Type*}
    [âˆ€ j i, TopologicalSpace (A j i)]
    {C : (j : n) â†’ (i : Î¹) â†’ Set (A j i)}
    (hCopen : âˆ€ j i, IsOpen (C j i))
    {f : (Î  j : n, Î Ê³ i : Î¹, [A j i, C j i]) â†’ X} :
    Continuous f â†”
      âˆ€ (S : Set Î¹) (hS : cofinite â‰¤ ğ“Ÿ S), Continuous (f âˆ˜ Pi.map fun _ â†¦ inclusion _ _ hS) := by
  refine âŸ¨by fun_prop, fun H â†¦ ?_âŸ©
  simp_rw [continuous_iff_continuousAt, ContinuousAt]
  intro x
  set S : Set Î¹ := {i | âˆ€ j, x j i âˆˆ C j i}
  have hS : cofinite â‰¤ ğ“Ÿ S := by
    rw [le_principal_iff]
    change âˆ€á¶  i in cofinite, âˆ€ j : n, x j i âˆˆ C j i
    simp [- eventually_cofinite]
  let x' (j : n) : Î Ê³ i : Î¹, [A j i, C j i]_[ğ“Ÿ S] := .mk (fun i â†¦ x j i) (fun i hi â†¦ hi _)
  have hxx' : Pi.map (fun j â†¦ inclusion _ _ hS) x' = x := rfl
  simp_rw [â† hxx', nhds_pi, Pi.map_apply, nhds_eq_map_inclusion (hCopen _), â† map_piMap_pi_finite,
    tendsto_map'_iff, â† nhds_pi]
  exact (H _ _).tendsto _

end cofinite

end Topology

section Compatibility
/-!
## Compatibility properties between algebra and topology
-/

variable {S : Î¹ â†’ Type*} -- subobject type
variable [Î  i, SetLike (S i) (R i)]
variable {B : Î  i, S i}
variable {T : Set Î¹} {ğ“• : Filter Î¹}
variable [Î  i, TopologicalSpace (R i)]

section general

@[to_additive]
instance [Î  i, Inv (R i)] [âˆ€ i, InvMemClass (S i) (R i)] [âˆ€ i, ContinuousInv (R i)] :
    ContinuousInv (Î Ê³ i, [R i, B i]_[ğ“•]) where
  continuous_inv := by
    rw [continuous_dom]
    intro T hT
    haveI : ContinuousInv (Î Ê³ i, [R i, B i]_[ğ“Ÿ T]) :=
      isEmbedding_coe_of_principal.continuousInv fun _ â†¦ rfl
    exact (continuous_inclusion hT).comp continuous_inv

@[to_additive]
instance {G : Type*} [Î  i, SMul G (R i)] [âˆ€ i, SMulMemClass (S i) G (R i)]
    [âˆ€ i, ContinuousConstSMul G (R i)] :
    ContinuousConstSMul G (Î Ê³ i, [R i, B i]_[ğ“•]) where
  continuous_const_smul g := by
    rw [continuous_dom]
    intro T hT
    haveI : ContinuousConstSMul G (Î Ê³ i, [R i, B i]_[ğ“Ÿ T]) :=
      isEmbedding_coe_of_principal.continuousConstSMul id rfl
    exact (continuous_inclusion hT).comp (continuous_const_smul g)

end general

section principal

@[to_additive]
instance [Î  i, Mul (R i)] [âˆ€ i, MulMemClass (S i) (R i)] [âˆ€ i, ContinuousMul (R i)] :
    ContinuousMul (Î Ê³ i, [R i, B i]_[ğ“Ÿ T]) :=
  let Ï† : Î Ê³ i, [R i, B i]_[ğ“Ÿ T] â†’â‚™* Î  i, R i :=
  { toFun := (â†‘)
    map_mul' := fun _ _ â†¦ rfl }
  isEmbedding_coe_of_principal.continuousMul Ï†

@[to_additive]
instance {G : Type*} [TopologicalSpace G] [Î  i, SMul G (R i)] [âˆ€ i, SMulMemClass (S i) G (R i)]
    [âˆ€ i, ContinuousSMul G (R i)] :
    ContinuousSMul G (Î Ê³ i, [R i, B i]_[ğ“Ÿ T]) :=
  isEmbedding_coe_of_principal.continuousSMul continuous_id rfl

@[to_additive]
instance [Î  i, Group (R i)] [âˆ€ i, SubgroupClass (S i) (R i)] [âˆ€ i, IsTopologicalGroup (R i)] :
    IsTopologicalGroup (Î Ê³ i, [R i, B i]_[ğ“Ÿ T]) where

instance [Î  i, Ring (R i)] [âˆ€ i, SubringClass (S i) (R i)] [âˆ€ i, IsTopologicalRing (R i)] :
    IsTopologicalRing (Î Ê³ i, [R i, B i]_[ğ“Ÿ T]) where

end principal

section cofinite

theorem nhds_zero_eq_map_ofPre [Î  i, Zero (R i)] [âˆ€ i, ZeroMemClass (S i) (R i)]
    (hBopen : âˆ€ i, IsOpen (B i : Set (R i))) (hT : cofinite â‰¤ ğ“Ÿ T) :
    (ğ“ (inclusion R (fun i â†¦ B i) hT 0)) = .map (inclusion R (fun i â†¦ B i) hT) (ğ“ 0) :=
  nhds_eq_map_inclusion hBopen hT 0

theorem nhds_zero_eq_map_structureMap [Î  i, Zero (R i)] [âˆ€ i, ZeroMemClass (S i) (R i)]
    (hBopen : âˆ€ i, IsOpen (B i : Set (R i))) :
    (ğ“ (structureMap R (fun i â†¦ B i) cofinite 0)) =
      .map (structureMap R (fun i â†¦ B i) cofinite) (ğ“ 0) :=
  nhds_eq_map_structureMap hBopen 0

-- TODO: Make `IsOpen` a class like `IsClosed` ?
variable [hBopen : Fact (âˆ€ i, IsOpen (B i : Set (R i)))]

@[to_additive]
instance [Î  i, Mul (R i)] [âˆ€ i, MulMemClass (S i) (R i)] [âˆ€ i, ContinuousMul (R i)] :
    ContinuousMul (Î Ê³ i, [R i, B i]) where
  continuous_mul := by
    rw [continuous_dom_prod hBopen.out hBopen.out]
    exact fun S hS â†¦ (continuous_inclusion hS).comp continuous_mul

@[to_additive]
instance continuousSMul {G : Type*} [TopologicalSpace G] [Î  i, SMul G (R i)]
    [âˆ€ i, SMulMemClass (S i) G (R i)] [âˆ€ i, ContinuousSMul G (R i)] :
    ContinuousSMul G (Î Ê³ i, [R i, B i]) where
  continuous_smul := by
    rw [continuous_dom_prod_left hBopen.out]
    exact fun S hS â†¦ (continuous_inclusion hS).comp continuous_smul

@[to_additive]
instance isTopologicalGroup [Î  i, Group (R i)] [âˆ€ i, SubgroupClass (S i) (R i)]
    [âˆ€ i, IsTopologicalGroup (R i)] :
    IsTopologicalGroup (Î Ê³ i, [R i, B i]) where

instance isTopologicalRing [Î  i, Ring (R i)] [âˆ€ i, SubringClass (S i) (R i)]
    [âˆ€ i, IsTopologicalRing (R i)] :
    IsTopologicalRing (Î Ê³ i, [R i, B i]) where

/-- Assume that each `R i` is a locally compact group with `A i` an open subgroup.
Assume also that all but finitely many `A i`s are compact.
Then the restricted product `Î Ê³ i, [R i, A i]` is a locally compact group. -/
@[to_additive
"Assume that each `R i` is a locally compact additive group with `A i` an open subgroup.
Assume also that all but finitely many `A i`s are compact.
Then the restricted product `Î Ê³ i, [R i, A i]` is a locally compact additive group."]
theorem locallyCompactSpace_of_group [Î  i, Group (R i)] [âˆ€ i, SubgroupClass (S i) (R i)]
    [âˆ€ i, IsTopologicalGroup (R i)] [âˆ€ i, LocallyCompactSpace (R i)]
    (hBcompact : âˆ€á¶  i in cofinite, IsCompact (B i : Set (R i))) :
    LocallyCompactSpace (Î Ê³ i, [R i, B i]) :=
  haveI : WeaklyLocallyCompactSpace (Î Ê³ i, [R i, B i]) :=
    weaklyLocallyCompactSpace_of_cofinite hBopen.out hBcompact
  inferInstance

open Pointwise in
@[to_additive]
instance [Î  i, Group (R i)] [âˆ€ i, SubgroupClass (S i) (R i)] [âˆ€ i, IsTopologicalGroup (R i)]
    [hAcompact : âˆ€ i, CompactSpace (B i)] : LocallyCompactSpace (Î Ê³ i, [R i, B i]) :=
  -- TODO: extract as a lemma
  haveI : âˆ€ i, WeaklyLocallyCompactSpace (R i) := fun i â†¦ .mk fun x â†¦
    âŸ¨x â€¢ (B i : Set (R i)), .smul _ (isCompact_iff_compactSpace.mpr inferInstance),
      hBopen.out i |>.smul _ |>.mem_nhds <| by
      simpa using smul_mem_smul_set (a := x) (one_mem (B i))âŸ©
  locallyCompactSpace_of_group _ <| .of_forall fun _ â†¦ isCompact_iff_compactSpace.mpr inferInstance

end cofinite

end Compatibility

section map_continuous

variable {Î¹â‚ Î¹â‚‚ : Type*}
variable (Râ‚ : Î¹â‚ â†’ Type*) (Râ‚‚ : Î¹â‚‚ â†’ Type*)
variable [âˆ€ i, TopologicalSpace (Râ‚ i)] [âˆ€ i, TopologicalSpace (Râ‚‚ i)]
variable {ğ“•â‚ : Filter Î¹â‚} {ğ“•â‚‚ : Filter Î¹â‚‚}
variable {Aâ‚ : (i : Î¹â‚) â†’ Set (Râ‚ i)} {Aâ‚‚ : (i : Î¹â‚‚) â†’ Set (Râ‚‚ i)}
variable (f : Î¹â‚‚ â†’ Î¹â‚) (hf : Tendsto f ğ“•â‚‚ ğ“•â‚)

variable (Ï† : âˆ€ j, Râ‚ (f j) â†’ Râ‚‚ j) (hÏ† : âˆ€á¶  j in ğ“•â‚‚, MapsTo (Ï† j) (Aâ‚ (f j)) (Aâ‚‚ j))

theorem map_continuous (Ï†_cont : âˆ€ j, Continuous (Ï† j)) : Continuous (map Râ‚ Râ‚‚ f hf Ï† hÏ†) := by
  rw [continuous_dom]
  intro S hS
  set T := f â»Â¹' S âˆ© {j | MapsTo (Ï† j) (Aâ‚ (f j)) (Aâ‚‚ j)}
  have hT : ğ“•â‚‚ â‰¤ ğ“Ÿ T := by
    rw [le_principal_iff] at hS âŠ¢
    exact inter_mem (hf hS) hÏ†
  have hf' : Tendsto f (ğ“Ÿ T) (ğ“Ÿ S) := by aesop
  have hÏ†' : âˆ€á¶  j in ğ“Ÿ T, MapsTo (Ï† j) (Aâ‚ (f j)) (Aâ‚‚ j) := by aesop
  have key : map Râ‚ Râ‚‚ f hf Ï† hÏ† âˆ˜ inclusion Râ‚ Aâ‚ hS =
      inclusion Râ‚‚ Aâ‚‚ hT âˆ˜ map Râ‚ Râ‚‚ f hf' Ï† hÏ†' := rfl
  rw [key]
  exact continuous_inclusion _ |>.comp <|
    continuous_rng_of_principal.mpr <|
    continuous_pi fun j â†¦ Ï†_cont j |>.comp <| continuous_eval (f j)

end map_continuous

end RestrictedProduct
