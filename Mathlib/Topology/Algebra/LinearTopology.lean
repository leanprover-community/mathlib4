/-
Copyright (c) 2024 Antoine Chambert-Loir, MarÃ­a InÃ©s de Frutos-FernÃ¡ndez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Antoine Chambert-Loir, MarÃ­a InÃ©s de Frutos-FernÃ¡ndez, Anatole Dedecker
-/

import Mathlib.RingTheory.TwoSidedIdeal.Operations
import Mathlib.Topology.Algebra.Ring.Basic
import Mathlib.Topology.Algebra.OpenSubgroup

/-! # Linear topologies on rings

Following Bourbaki, *Algebra II*, chapter 4, Â§2, nÂ° 3, a topology on a ring `R` is *linear* if
it is invariant by translation and admits a basis of neighborhoods of 0 consisting of
two-sided ideals.

- `tendsto_mul_zero_of_left`: for `f, g : Î¹ â†’ R` such that `f i` converges to `0`,
`f i * g i` converges to `0`.

- `tendsto_mul_zero_of_right`: for `f, g : Î¹ â†’ R` such that `g i` converges to `0`,
`f i * g i` converges to `0`.

## Instances

- A discrete topology is a linear topology

## Note on the implementation

The definition of Bourbaki doesn't presuppose, but implies, that a linear topology on a ring `R` is
a ring topology. However, in some of our lemmas, we already assume that `R` is a topological ring.
This unnecessary assumption will be made unnecessary by results in the ongoing PR #18437.
Anyway, the idea will be to first define a topology on `R`, and then
prove that it makes `R` a topological ring, and that it is a linear topology.

-/

open scoped Topology
open Filter

namespace IsLinearTopology

section Ring

/-- A topology on a ring is linear if its topology is defined by a family of ideals. -/
class _root_.IsLinearTopology (R : Type*) [Ring R] [TopologicalSpace R] where
  hasBasis_twoSidedIdeal : (ğ“ (0 : R)).HasBasis
    (fun I : TwoSidedIdeal R â†¦ (I : Set R) âˆˆ ğ“ 0) (fun I : TwoSidedIdeal R â†¦ (I : Set R))

variable {R : Type*} [Ring R] [TopologicalSpace R]

lemma hasBasis_open_twoSidedIdeal [TopologicalRing R] [IsLinearTopology R] :
    (ğ“ (0 : R)).HasBasis
      (fun I : TwoSidedIdeal R â†¦ IsOpen (I : Set R)) (fun I : TwoSidedIdeal R â†¦ (I : Set R)) :=
  hasBasis_twoSidedIdeal.congr
    (fun I â†¦ âŸ¨I.asIdeal.toAddSubgroup.isOpen_of_mem_nhds, fun hI â†¦ hI.mem_nhds (zero_mem I)âŸ©)
    (fun _ _ â†¦ rfl)

theorem hasBasis_ideal [IsLinearTopology R] :
    (ğ“ 0).HasBasis (fun I : Ideal R â†¦ (I : Set R) âˆˆ ğ“ 0) (fun I : Ideal R â†¦ (I : Set R)) :=
  hasBasis_twoSidedIdeal.to_hasBasis
    (fun I hI â†¦ âŸ¨I.asIdeal, hI, subset_rflâŸ©)
    (fun _ â†¦ hasBasis_twoSidedIdeal.mem_iff.mp)

theorem hasBasis_open_ideal [TopologicalRing R] [IsLinearTopology R] :
    (ğ“ 0).HasBasis (fun I : Ideal R â†¦ IsOpen (I : Set R)) (fun I : Ideal R â†¦ (I : Set R)) :=
  hasBasis_ideal.congr
    (fun I â†¦ âŸ¨I.toAddSubgroup.isOpen_of_mem_nhds, fun hI â†¦ hI.mem_nhds (zero_mem I)âŸ©)
    (fun _ _ â†¦ rfl)

lemma mk_of_twoSidedIdeal {Î¹ : Sort*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ TwoSidedIdeal R}
    (h : (ğ“ 0).HasBasis p (fun i â†¦ (s i : Set R))) :
    IsLinearTopology R where
  hasBasis_twoSidedIdeal := h.to_hasBasis (fun i hi â†¦ âŸ¨s i, h.mem_of_mem hi, subset_rflâŸ©)
    (fun _ â†¦ h.mem_iff.mp)

theorem _root_.isLinearTopology_iff_hasBasis_twoSidedIdeal :
    IsLinearTopology R â†” (ğ“ 0).HasBasis
      (fun I : TwoSidedIdeal R â†¦ (I : Set R) âˆˆ ğ“ 0) (fun I : TwoSidedIdeal R â†¦ (I : Set R)) :=
  âŸ¨fun _ â†¦ hasBasis_twoSidedIdeal, fun h â†¦ .mk_of_twoSidedIdeal hâŸ©

theorem _root_.isLinearTopology_iff_hasBasis_open_twoSidedIdeal [TopologicalRing R] :
    IsLinearTopology R â†” (ğ“ 0).HasBasis
      (fun I : TwoSidedIdeal R â†¦ IsOpen (I : Set R)) (fun I : TwoSidedIdeal R â†¦ (I : Set R)) :=
  âŸ¨fun _ â†¦ hasBasis_open_twoSidedIdeal, fun h â†¦ .mk_of_twoSidedIdeal hâŸ©

instance [DiscreteTopology R] : IsLinearTopology R :=
  have : HasBasis (ğ“ 0 : Filter R) (fun _ â†¦ True) (fun (_ : Unit) â†¦ (âŠ¥ : TwoSidedIdeal R)) := by
    rw [nhds_discrete]
    exact hasBasis_pure _
  mk_of_twoSidedIdeal this

theorem tendsto_mul_zero_of_left [IsLinearTopology R] {Î¹ : Type*} {f : Filter Î¹}
    (a b : Î¹ â†’ R) (ha : Tendsto a f (ğ“ 0)) :
    Tendsto (a * b) f (ğ“ 0) := by
  rw [hasBasis_twoSidedIdeal.tendsto_right_iff] at ha âŠ¢
  intro I hI
  filter_upwards [ha I hI] with i ai_mem
  exact I.mul_mem_right _ _ ai_mem

theorem tendsto_mul_zero_of_right [IsLinearTopology R] {Î¹ : Type*} {f : Filter Î¹}
    (a b : Î¹ â†’ R) (hb : Tendsto b f (ğ“ 0)) :
    Tendsto (a * b) f (ğ“ 0) := by
  rw [hasBasis_twoSidedIdeal.tendsto_right_iff] at hb âŠ¢
  intro I hI
  filter_upwards [hb I hI] with i bi_mem
  exact I.mul_mem_left _ _ bi_mem

end Ring

section CommRing

variable {R : Type*} [CommRing R] [TopologicalSpace R]

lemma mk_of_ideal {Î¹ : Sort*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Ideal R}
    (h : (ğ“ 0).HasBasis p (fun i â†¦ (s i : Set R))) :
    IsLinearTopology R where
  hasBasis_twoSidedIdeal := h.to_hasBasis
    (fun i hi â†¦ âŸ¨(s i).toTwoSided ((s i).mul_mem_right _), by simpa using h.mem_of_mem hi, by simpâŸ©)
    (fun _ â†¦ h.mem_iff.mp)

theorem _root_.isLinearTopology_iff_hasBasis_ideal :
    IsLinearTopology R â†” (ğ“ 0).HasBasis
      (fun I : Ideal R â†¦ (I : Set R) âˆˆ ğ“ 0) (fun I : Ideal R â†¦ (I : Set R)) :=
  âŸ¨fun _ â†¦ hasBasis_ideal, fun h â†¦ .mk_of_ideal hâŸ©

theorem _root_.isLinearTopology_iff_hasBasis_open_ideal [TopologicalRing R] :
    IsLinearTopology R â†” (ğ“ 0).HasBasis
      (fun I : Ideal R â†¦ IsOpen (I : Set R)) (fun I : Ideal R â†¦ (I : Set R)) :=
  âŸ¨fun _ â†¦ hasBasis_open_ideal, fun h â†¦ .mk_of_ideal hâŸ©

end CommRing

end IsLinearTopology
