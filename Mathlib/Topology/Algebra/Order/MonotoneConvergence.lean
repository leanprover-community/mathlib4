/-
Copyright (c) 2021 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth, Yury Kudryashov
-/
import Mathlib.Topology.Order.Basic

#align_import topology.algebra.order.monotone_convergence from "leanprover-community/mathlib"@"4c19a16e4b705bf135cf9a80ac18fcc99c438514"

/-!
# Bounded monotone sequences converge

In this file we prove a few theorems of the form â€œif the range of a monotone function `f : Î¹ â†’ Î±`
admits a least upper bound `a`, then `f x` tends to `a` as `x â†’ âˆ`â€, as well as version of this
statement for (conditionally) complete lattices that use `â¨† x, f x` instead of `IsLUB`.

These theorems work for linear orders with order topologies as well as their products (both in terms
of `Prod` and in terms of function types). In order to reduce code duplication, we introduce two
typeclasses (one for the property formulated above and one for the dual property), prove theorems
assuming one of these typeclasses, and provide instances for linear orders and their products.

We also prove some "inverse" results: if `f n` is a monotone sequence and `a` is its limit,
then `f n â‰¤ a` for all `n`.

## Tags

monotone convergence
-/


open Filter Set Function

open Filter Topology Classical

variable {Î± Î² : Type*}

/-- We say that `Î±` is a `SupConvergenceClass` if the following holds. Let `f : Î¹ â†’ Î±` be a
monotone function, let `a : Î±` be a least upper bound of `Set.range f`. Then `f x` tends to `ğ“ a`
 as `x â†’ âˆ` (formally, at the filter `Filter.atTop`). We require this for `Î¹ = (s : Set Î±)`,
`f = CoeTC.coe` in the definition, then prove it for any `f` in `tendsto_atTop_isLUB`.

This property holds for linear orders with order topology as well as their products. -/
class SupConvergenceClass (Î± : Type*) [Preorder Î±] [TopologicalSpace Î±] : Prop where
  /-- proof that a monotone function tends to `ğ“ a` as `x â†’ âˆ` -/
  tendsto_coe_atTop_isLUB :
    âˆ€ (a : Î±) (s : Set Î±), IsLUB s a â†’ Tendsto (CoeTC.coe : s â†’ Î±) atTop (ğ“ a)
#align Sup_convergence_class SupConvergenceClass

/-- We say that `Î±` is an `InfConvergenceClass` if the following holds. Let `f : Î¹ â†’ Î±` be a
monotone function, let `a : Î±` be a greatest lower bound of `Set.range f`. Then `f x` tends to `ğ“ a`
as `x â†’ -âˆ` (formally, at the filter `Filter.atBot`). We require this for `Î¹ = (s : Set Î±)`,
`f = CoeTC.coe` in the definition, then prove it for any `f` in `tendsto_atBot_isGLB`.

This property holds for linear orders with order topology as well as their products. -/
class InfConvergenceClass (Î± : Type*) [Preorder Î±] [TopologicalSpace Î±] : Prop where
  /-- proof that a monotone function tends to `ğ“ a` as `x â†’ -âˆ`-/
  tendsto_coe_atBot_isGLB :
    âˆ€ (a : Î±) (s : Set Î±), IsGLB s a â†’ Tendsto (CoeTC.coe : s â†’ Î±) atBot (ğ“ a)
#align Inf_convergence_class InfConvergenceClass

instance OrderDual.supConvergenceClass [Preorder Î±] [TopologicalSpace Î±] [InfConvergenceClass Î±] :
    SupConvergenceClass Î±áµ’áµˆ :=
  âŸ¨â€¹InfConvergenceClass Î±â€º.1âŸ©
#align order_dual.Sup_convergence_class OrderDual.supConvergenceClass

instance OrderDual.infConvergenceClass [Preorder Î±] [TopologicalSpace Î±] [SupConvergenceClass Î±] :
    InfConvergenceClass Î±áµ’áµˆ :=
  âŸ¨â€¹SupConvergenceClass Î±â€º.1âŸ©
#align order_dual.Inf_convergence_class OrderDual.infConvergenceClass

-- see Note [lower instance priority]
instance (priority := 100) LinearOrder.supConvergenceClass [TopologicalSpace Î±] [LinearOrder Î±]
    [OrderTopology Î±] : SupConvergenceClass Î± := by
  refine' âŸ¨fun a s ha => tendsto_order.2 âŸ¨fun b hb => _, fun b hb => _âŸ©âŸ©
  -- âŠ¢ âˆ€á¶  (b_1 : â†‘s) in atTop, b < CoeTC.coe b_1
  Â· rcases ha.exists_between hb with âŸ¨c, hcs, bc, bcaâŸ©
    -- âŠ¢ âˆ€á¶  (b_1 : â†‘s) in atTop, b < CoeTC.coe b_1
    lift c to s using hcs
    -- âŠ¢ âˆ€á¶  (b_1 : â†‘s) in atTop, b < CoeTC.coe b_1
    refine' (eventually_ge_atTop c).mono fun x hx => bc.trans_le hx
    -- ğŸ‰ no goals
  Â· exact eventually_of_forall fun x => (ha.1 x.2).trans_lt hb
    -- ğŸ‰ no goals
#align linear_order.Sup_convergence_class LinearOrder.supConvergenceClass

-- see Note [lower instance priority]
instance (priority := 100) LinearOrder.infConvergenceClass [TopologicalSpace Î±] [LinearOrder Î±]
    [OrderTopology Î±] : InfConvergenceClass Î± :=
  show InfConvergenceClass Î±áµ’áµˆáµ’áµˆ from OrderDual.infConvergenceClass
#align linear_order.Inf_convergence_class LinearOrder.infConvergenceClass

section

variable {Î¹ : Type*} [Preorder Î¹] [TopologicalSpace Î±]

section IsLUB

variable [Preorder Î±] [SupConvergenceClass Î±] {f : Î¹ â†’ Î±} {a : Î±}

theorem tendsto_atTop_isLUB (h_mono : Monotone f) (ha : IsLUB (Set.range f) a) :
    Tendsto f atTop (ğ“ a) := by
  suffices : Tendsto (rangeFactorization f) atTop atTop
  -- âŠ¢ Tendsto f atTop (ğ“ a)
  exact (SupConvergenceClass.tendsto_coe_atTop_isLUB _ _ ha).comp this
  -- âŠ¢ Tendsto (rangeFactorization f) atTop atTop
  exact h_mono.rangeFactorization.tendsto_atTop_atTop fun b => b.2.imp fun a ha => ha.ge
  -- ğŸ‰ no goals
#align tendsto_at_top_is_lub tendsto_atTop_isLUB

theorem tendsto_atBot_isLUB (h_anti : Antitone f) (ha : IsLUB (Set.range f) a) :
    Tendsto f atBot (ğ“ a) := by convert tendsto_atTop_isLUB h_anti.dual_left ha using 1
                                -- ğŸ‰ no goals
#align tendsto_at_bot_is_lub tendsto_atBot_isLUB

end IsLUB

section IsGLB

variable [Preorder Î±] [InfConvergenceClass Î±] {f : Î¹ â†’ Î±} {a : Î±}

theorem tendsto_atBot_isGLB (h_mono : Monotone f) (ha : IsGLB (Set.range f) a) :
    Tendsto f atBot (ğ“ a) := by convert tendsto_atTop_isLUB h_mono.dual ha.dual using 1
                                -- ğŸ‰ no goals
#align tendsto_at_bot_is_glb tendsto_atBot_isGLB

theorem tendsto_atTop_isGLB (h_anti : Antitone f) (ha : IsGLB (Set.range f) a) :
    Tendsto f atTop (ğ“ a) := by convert tendsto_atBot_isLUB h_anti.dual ha.dual using 1
                                -- ğŸ‰ no goals
#align tendsto_at_top_is_glb tendsto_atTop_isGLB

end IsGLB

section CiSup

variable [ConditionallyCompleteLattice Î±] [SupConvergenceClass Î±] {f : Î¹ â†’ Î±} {a : Î±}

theorem tendsto_atTop_ciSup (h_mono : Monotone f) (hbdd : BddAbove <| range f) :
    Tendsto f atTop (ğ“ (â¨† i, f i)) := by
  cases isEmpty_or_nonempty Î¹
  -- âŠ¢ Tendsto f atTop (ğ“ (â¨† (i : Î¹), f i))
  exacts [tendsto_of_isEmpty, tendsto_atTop_isLUB h_mono (isLUB_ciSup hbdd)]
  -- ğŸ‰ no goals
#align tendsto_at_top_csupr tendsto_atTop_ciSup

theorem tendsto_atBot_ciSup (h_anti : Antitone f) (hbdd : BddAbove <| range f) :
    Tendsto f atBot (ğ“ (â¨† i, f i)) := by convert tendsto_atTop_ciSup h_anti.dual hbdd.dual using 1
                                         -- ğŸ‰ no goals
#align tendsto_at_bot_csupr tendsto_atBot_ciSup

end CiSup

section CiInf

variable [ConditionallyCompleteLattice Î±] [InfConvergenceClass Î±] {f : Î¹ â†’ Î±} {a : Î±}

theorem tendsto_atBot_ciInf (h_mono : Monotone f) (hbdd : BddBelow <| range f) :
    Tendsto f atBot (ğ“ (â¨… i, f i)) := by convert tendsto_atTop_ciSup h_mono.dual hbdd.dual using 1
                                         -- ğŸ‰ no goals
#align tendsto_at_bot_cinfi tendsto_atBot_ciInf

theorem tendsto_atTop_ciInf (h_anti : Antitone f) (hbdd : BddBelow <| range f) :
    Tendsto f atTop (ğ“ (â¨… i, f i)) := by convert tendsto_atBot_ciSup h_anti.dual hbdd.dual using 1
                                         -- ğŸ‰ no goals
#align tendsto_at_top_cinfi tendsto_atTop_ciInf

end CiInf

section iSup

variable [CompleteLattice Î±] [SupConvergenceClass Î±] {f : Î¹ â†’ Î±} {a : Î±}

theorem tendsto_atTop_iSup (h_mono : Monotone f) : Tendsto f atTop (ğ“ (â¨† i, f i)) :=
  tendsto_atTop_ciSup h_mono (OrderTop.bddAbove _)
#align tendsto_at_top_supr tendsto_atTop_iSup

theorem tendsto_atBot_iSup (h_anti : Antitone f) : Tendsto f atBot (ğ“ (â¨† i, f i)) :=
  tendsto_atBot_ciSup h_anti (OrderTop.bddAbove _)
#align tendsto_at_bot_supr tendsto_atBot_iSup

end iSup

section iInf

variable [CompleteLattice Î±] [InfConvergenceClass Î±] {f : Î¹ â†’ Î±} {a : Î±}

theorem tendsto_atBot_iInf (h_mono : Monotone f) : Tendsto f atBot (ğ“ (â¨… i, f i)) :=
  tendsto_atBot_ciInf h_mono (OrderBot.bddBelow _)
#align tendsto_at_bot_infi tendsto_atBot_iInf

theorem tendsto_atTop_iInf (h_anti : Antitone f) : Tendsto f atTop (ğ“ (â¨… i, f i)) :=
  tendsto_atTop_ciInf h_anti (OrderBot.bddBelow _)
#align tendsto_at_top_infi tendsto_atTop_iInf

end iInf

end

instance Prod.supConvergenceClass
    [Preorder Î±] [Preorder Î²] [TopologicalSpace Î±] [TopologicalSpace Î²]
    [SupConvergenceClass Î±] [SupConvergenceClass Î²] : SupConvergenceClass (Î± Ã— Î²) := by
  constructor
  -- âŠ¢ âˆ€ (a : Î± Ã— Î²) (s : Set (Î± Ã— Î²)), IsLUB s a â†’ Tendsto CoeTC.coe atTop (ğ“ a)
  rintro âŸ¨a, bâŸ© s h
  -- âŠ¢ Tendsto CoeTC.coe atTop (ğ“ (a, b))
  rw [isLUB_prod, â† range_restrict, â† range_restrict] at h
  -- âŠ¢ Tendsto CoeTC.coe atTop (ğ“ (a, b))
  have A : Tendsto (fun x : s => (x : Î± Ã— Î²).1) atTop (ğ“ a) :=
    tendsto_atTop_isLUB (monotone_fst.restrict s) h.1
  have B : Tendsto (fun x : s => (x : Î± Ã— Î²).2) atTop (ğ“ b) :=
    tendsto_atTop_isLUB (monotone_snd.restrict s) h.2
  convert A.prod_mk_nhds B
  -- ğŸ‰ no goals
  -- porting note: previously required below to close
  -- ext1 âŸ¨âŸ¨x, yâŸ©, hâŸ©
  -- rfl

instance [Preorder Î±] [Preorder Î²] [TopologicalSpace Î±] [TopologicalSpace Î²] [InfConvergenceClass Î±]
    [InfConvergenceClass Î²] : InfConvergenceClass (Î± Ã— Î²) :=
  show InfConvergenceClass (Î±áµ’áµˆ Ã— Î²áµ’áµˆ)áµ’áµˆ from OrderDual.infConvergenceClass

instance Pi.supConvergenceClass
    {Î¹ : Type*} {Î± : Î¹ â†’ Type*} [âˆ€ i, Preorder (Î± i)] [âˆ€ i, TopologicalSpace (Î± i)]
    [âˆ€ i, SupConvergenceClass (Î± i)] : SupConvergenceClass (âˆ€ i, Î± i) := by
  refine' âŸ¨fun f s h => _âŸ©
  -- âŠ¢ Tendsto CoeTC.coe atTop (ğ“ f)
  simp only [isLUB_pi, â† range_restrict] at h
  -- âŠ¢ Tendsto CoeTC.coe atTop (ğ“ f)
  exact tendsto_pi_nhds.2 fun i => tendsto_atTop_isLUB ((monotone_eval _).restrict _) (h i)
  -- ğŸ‰ no goals

instance Pi.infConvergenceClass
    {Î¹ : Type*} {Î± : Î¹ â†’ Type*} [âˆ€ i, Preorder (Î± i)] [âˆ€ i, TopologicalSpace (Î± i)]
    [âˆ€ i, InfConvergenceClass (Î± i)] : InfConvergenceClass (âˆ€ i, Î± i) :=
  show InfConvergenceClass (âˆ€ i, (Î± i)áµ’áµˆ)áµ’áµˆ from OrderDual.infConvergenceClass

instance Pi.supConvergenceClass' {Î¹ : Type*} [Preorder Î±] [TopologicalSpace Î±]
    [SupConvergenceClass Î±] : SupConvergenceClass (Î¹ â†’ Î±) :=
  supConvergenceClass
#align pi.Sup_convergence_class' Pi.supConvergenceClass'

instance Pi.infConvergenceClass' {Î¹ : Type*} [Preorder Î±] [TopologicalSpace Î±]
    [InfConvergenceClass Î±] : InfConvergenceClass (Î¹ â†’ Î±) :=
  Pi.infConvergenceClass
#align pi.Inf_convergence_class' Pi.infConvergenceClass'

theorem tendsto_of_monotone {Î¹ Î± : Type*} [Preorder Î¹] [TopologicalSpace Î±]
    [ConditionallyCompleteLinearOrder Î±] [OrderTopology Î±] {f : Î¹ â†’ Î±} (h_mono : Monotone f) :
    Tendsto f atTop atTop âˆ¨ âˆƒ l, Tendsto f atTop (ğ“ l) :=
  if H : BddAbove (range f) then Or.inr âŸ¨_, tendsto_atTop_ciSup h_mono HâŸ©
  else Or.inl <| tendsto_atTop_atTop_of_monotone' h_mono H
#align tendsto_of_monotone tendsto_of_monotone

theorem tendsto_iff_tendsto_subseq_of_monotone {Î¹â‚ Î¹â‚‚ Î± : Type*} [SemilatticeSup Î¹â‚] [Preorder Î¹â‚‚]
    [Nonempty Î¹â‚] [TopologicalSpace Î±] [ConditionallyCompleteLinearOrder Î±] [OrderTopology Î±]
    [NoMaxOrder Î±] {f : Î¹â‚‚ â†’ Î±} {Ï† : Î¹â‚ â†’ Î¹â‚‚} {l : Î±} (hf : Monotone f)
    (hg : Tendsto Ï† atTop atTop) : Tendsto f atTop (ğ“ l) â†” Tendsto (f âˆ˜ Ï†) atTop (ğ“ l) := by
  constructor <;> intro h
  -- âŠ¢ Tendsto f atTop (ğ“ l) â†’ Tendsto (f âˆ˜ Ï†) atTop (ğ“ l)
                  -- âŠ¢ Tendsto (f âˆ˜ Ï†) atTop (ğ“ l)
                  -- âŠ¢ Tendsto f atTop (ğ“ l)
  Â· exact h.comp hg
    -- ğŸ‰ no goals
  Â· rcases tendsto_of_monotone hf with (h' | âŸ¨l', hl'âŸ©)
    -- âŠ¢ Tendsto f atTop (ğ“ l)
    Â· exact (not_tendsto_atTop_of_tendsto_nhds h (h'.comp hg)).elim
      -- ğŸ‰ no goals
    Â· rwa [tendsto_nhds_unique h (hl'.comp hg)]
      -- ğŸ‰ no goals
#align tendsto_iff_tendsto_subseq_of_monotone tendsto_iff_tendsto_subseq_of_monotone

/-! The next family of results, such as `isLUB_of_tendsto_atTop` and `iSup_eq_of_tendsto`, are
converses to the standard fact that bounded monotone functions converge. They state, that if a
monotone function `f` tends to `a` along `Filter.atTop`, then that value `a` is a least upper bound
for the range of `f`.

Related theorems above (`IsLUB.isLUB_of_tendsto`, `IsGLB.isGLB_of_tendsto` etc) cover the case
when `f x` tends to `a` as `x` tends to some point `b` in the domain. -/

theorem Monotone.ge_of_tendsto [TopologicalSpace Î±] [Preorder Î±] [OrderClosedTopology Î±]
    [SemilatticeSup Î²] {f : Î² â†’ Î±} {a : Î±} (hf : Monotone f) (ha : Tendsto f atTop (ğ“ a)) (b : Î²) :
    f b â‰¤ a :=
  haveI : Nonempty Î² := Nonempty.intro b
  _root_.ge_of_tendsto ha ((eventually_ge_atTop b).mono fun _ hxy => hf hxy)
#align monotone.ge_of_tendsto Monotone.ge_of_tendsto

theorem Monotone.le_of_tendsto [TopologicalSpace Î±] [Preorder Î±] [OrderClosedTopology Î±]
    [SemilatticeInf Î²] {f : Î² â†’ Î±} {a : Î±} (hf : Monotone f) (ha : Tendsto f atBot (ğ“ a)) (b : Î²) :
    a â‰¤ f b :=
  hf.dual.ge_of_tendsto ha b
#align monotone.le_of_tendsto Monotone.le_of_tendsto

theorem Antitone.le_of_tendsto [TopologicalSpace Î±] [Preorder Î±] [OrderClosedTopology Î±]
    [SemilatticeSup Î²] {f : Î² â†’ Î±} {a : Î±} (hf : Antitone f) (ha : Tendsto f atTop (ğ“ a)) (b : Î²) :
    a â‰¤ f b :=
  hf.dual_right.ge_of_tendsto ha b
#align antitone.le_of_tendsto Antitone.le_of_tendsto

theorem Antitone.ge_of_tendsto [TopologicalSpace Î±] [Preorder Î±] [OrderClosedTopology Î±]
    [SemilatticeInf Î²] {f : Î² â†’ Î±} {a : Î±} (hf : Antitone f) (ha : Tendsto f atBot (ğ“ a)) (b : Î²) :
    f b â‰¤ a :=
  hf.dual_right.le_of_tendsto ha b
#align antitone.ge_of_tendsto Antitone.ge_of_tendsto

theorem isLUB_of_tendsto_atTop [TopologicalSpace Î±] [Preorder Î±] [OrderClosedTopology Î±]
    [Nonempty Î²] [SemilatticeSup Î²] {f : Î² â†’ Î±} {a : Î±} (hf : Monotone f)
    (ha : Tendsto f atTop (ğ“ a)) : IsLUB (Set.range f) a := by
  constructor
  -- âŠ¢ a âˆˆ upperBounds (range f)
  Â· rintro _ âŸ¨b, rflâŸ©
    -- âŠ¢ f b â‰¤ a
    exact hf.ge_of_tendsto ha b
    -- ğŸ‰ no goals
  Â· exact fun _ hb => le_of_tendsto' ha fun x => hb (Set.mem_range_self x)
    -- ğŸ‰ no goals
#align is_lub_of_tendsto_at_top isLUB_of_tendsto_atTop

theorem isGLB_of_tendsto_atBot [TopologicalSpace Î±] [Preorder Î±] [OrderClosedTopology Î±]
    [Nonempty Î²] [SemilatticeInf Î²] {f : Î² â†’ Î±} {a : Î±} (hf : Monotone f)
    (ha : Tendsto f atBot (ğ“ a)) : IsGLB (Set.range f) a :=
  @isLUB_of_tendsto_atTop Î±áµ’áµˆ Î²áµ’áµˆ _ _ _ _ _ _ _ hf.dual ha
#align is_glb_of_tendsto_at_bot isGLB_of_tendsto_atBot

theorem isLUB_of_tendsto_atBot [TopologicalSpace Î±] [Preorder Î±] [OrderClosedTopology Î±]
    [Nonempty Î²] [SemilatticeInf Î²] {f : Î² â†’ Î±} {a : Î±} (hf : Antitone f)
    (ha : Tendsto f atBot (ğ“ a)) : IsLUB (Set.range f) a :=
  @isLUB_of_tendsto_atTop Î± Î²áµ’áµˆ _ _ _ _ _ _ _ hf.dual_left ha
#align is_lub_of_tendsto_at_bot isLUB_of_tendsto_atBot

theorem isGLB_of_tendsto_atTop [TopologicalSpace Î±] [Preorder Î±] [OrderClosedTopology Î±]
    [Nonempty Î²] [SemilatticeSup Î²] {f : Î² â†’ Î±} {a : Î±} (hf : Antitone f)
    (ha : Tendsto f atTop (ğ“ a)) : IsGLB (Set.range f) a :=
  @isGLB_of_tendsto_atBot Î± Î²áµ’áµˆ _ _ _ _ _ _ _ hf.dual_left ha
#align is_glb_of_tendsto_at_top isGLB_of_tendsto_atTop

theorem iSup_eq_of_tendsto {Î± Î²} [TopologicalSpace Î±] [CompleteLinearOrder Î±] [OrderTopology Î±]
    [Nonempty Î²] [SemilatticeSup Î²] {f : Î² â†’ Î±} {a : Î±} (hf : Monotone f) :
    Tendsto f atTop (ğ“ a) â†’ iSup f = a :=
  tendsto_nhds_unique (tendsto_atTop_iSup hf)
#align supr_eq_of_tendsto iSup_eq_of_tendsto

theorem iInf_eq_of_tendsto {Î±} [TopologicalSpace Î±] [CompleteLinearOrder Î±] [OrderTopology Î±]
    [Nonempty Î²] [SemilatticeSup Î²] {f : Î² â†’ Î±} {a : Î±} (hf : Antitone f) :
    Tendsto f atTop (ğ“ a) â†’ iInf f = a :=
  tendsto_nhds_unique (tendsto_atTop_iInf hf)
#align infi_eq_of_tendsto iInf_eq_of_tendsto

theorem iSup_eq_iSup_subseq_of_monotone {Î¹â‚ Î¹â‚‚ Î± : Type*} [Preorder Î¹â‚‚] [CompleteLattice Î±]
    {l : Filter Î¹â‚} [l.NeBot] {f : Î¹â‚‚ â†’ Î±} {Ï† : Î¹â‚ â†’ Î¹â‚‚} (hf : Monotone f)
    (hÏ† : Tendsto Ï† l atTop) : â¨† i, f i = â¨† i, f (Ï† i) :=
  le_antisymm
    (iSup_mono' fun i =>
      Exists.imp (fun j (hj : i â‰¤ Ï† j) => hf hj) (hÏ†.eventually <| eventually_ge_atTop i).exists)
    (iSup_mono' fun i => âŸ¨Ï† i, le_rflâŸ©)
#align supr_eq_supr_subseq_of_monotone iSup_eq_iSup_subseq_of_monotone

theorem iSup_eq_iSup_subseq_of_antitone {Î¹â‚ Î¹â‚‚ Î± : Type*} [Preorder Î¹â‚‚] [CompleteLattice Î±]
    {l : Filter Î¹â‚} [l.NeBot] {f : Î¹â‚‚ â†’ Î±} {Ï† : Î¹â‚ â†’ Î¹â‚‚} (hf : Antitone f)
    (hÏ† : Tendsto Ï† l atBot) : â¨† i, f i = â¨† i, f (Ï† i) :=
  le_antisymm
    (iSup_mono' fun i =>
      Exists.imp (fun j (hj : Ï† j â‰¤ i) => hf hj) (hÏ†.eventually <| eventually_le_atBot i).exists)
    (iSup_mono' fun i => âŸ¨Ï† i, le_rflâŸ©)
#align supr_eq_supr_subseq_of_antitone iSup_eq_iSup_subseq_of_antitone

theorem iInf_eq_iInf_subseq_of_monotone {Î¹â‚ Î¹â‚‚ Î± : Type*} [Preorder Î¹â‚‚] [CompleteLattice Î±]
    {l : Filter Î¹â‚} [l.NeBot] {f : Î¹â‚‚ â†’ Î±} {Ï† : Î¹â‚ â†’ Î¹â‚‚} (hf : Monotone f)
    (hÏ† : Tendsto Ï† l atBot) : â¨… i, f i = â¨… i, f (Ï† i) :=
  iSup_eq_iSup_subseq_of_monotone hf.dual hÏ†
#align infi_eq_infi_subseq_of_monotone iInf_eq_iInf_subseq_of_monotone

theorem iInf_eq_iInf_subseq_of_antitone {Î¹â‚ Î¹â‚‚ Î± : Type*} [Preorder Î¹â‚‚] [CompleteLattice Î±]
    {l : Filter Î¹â‚} [l.NeBot] {f : Î¹â‚‚ â†’ Î±} {Ï† : Î¹â‚ â†’ Î¹â‚‚} (hf : Antitone f)
    (hÏ† : Tendsto Ï† l atTop) : â¨… i, f i = â¨… i, f (Ï† i) :=
  iSup_eq_iSup_subseq_of_antitone hf.dual hÏ†
