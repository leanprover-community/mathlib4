/-
Copyright (c) 2022 Benjamin Davidson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Benjamin Davidson, Devon Tuma, Eric Rodriguez, Oliver Nash
-/
import Mathlib.Data.Set.Pointwise.Interval
import Mathlib.Topology.Algebra.Field
import Mathlib.Topology.Algebra.Order.Group

#align_import topology.algebra.order.field from "leanprover-community/mathlib"@"9a59dcb7a2d06bf55da57b9030169219980660cd"

/-!
# Topologies on linear ordered fields

In this file we prove that a linear ordered field with order topology has continuous multiplication
and division (apart from zero in the denominator). We also prove theorems like
`Filter.Tendsto.mul_atTop`: if `f` tends to a positive number and `g` tends to positive infinity,
then `f * g` tends to positive infinity.
-/


open Set Filter TopologicalSpace Function
open scoped Pointwise Topology
open OrderDual (toDual ofDual)

/-- If a (possibly non-unital and/or non-associative) ring `R` admits a submultiplicative
nonnegative norm `norm : R â†’ ğ•œ`, where `ğ•œ` is a linear ordered field, and the open balls
`{ x | norm x < Îµ }`, `Îµ > 0`, form a basis of neighborhoods of zero, then `R` is a topological
ring. -/
theorem TopologicalRing.of_norm {R ğ•œ : Type*} [NonUnitalNonAssocRing R] [LinearOrderedField ğ•œ]
    [TopologicalSpace R] [TopologicalAddGroup R] (norm : R â†’ ğ•œ)
    (norm_nonneg : âˆ€ x, 0 â‰¤ norm x) (norm_mul_le : âˆ€ x y, norm (x * y) â‰¤ norm x * norm y)
    (nhds_basis : (ğ“ (0 : R)).HasBasis ((0 : ğ•œ) < Â·) (fun Îµ â†¦ { x | norm x < Îµ })) :
    TopologicalRing R := by
  have h0 : âˆ€ f : R â†’ R, âˆ€ c â‰¥ (0 : ğ•œ), (âˆ€ x, norm (f x) â‰¤ c * norm x) â†’
      Tendsto f (ğ“ 0) (ğ“ 0) := by
    refine fun f c c0 hf â†¦ (nhds_basis.tendsto_iff nhds_basis).2 fun Îµ Îµ0 â†¦ ?_
    rcases exists_pos_mul_lt Îµ0 c with âŸ¨Î´, Î´0, hÎ´âŸ©
    refine âŸ¨Î´, Î´0, fun x hx â†¦ (hf _).trans_lt ?_âŸ©
    exact (mul_le_mul_of_nonneg_left (le_of_lt hx) c0).trans_lt hÎ´
  apply TopologicalRing.of_addGroup_of_nhds_zero
  case hmul =>
    refine ((nhds_basis.prod nhds_basis).tendsto_iff nhds_basis).2 fun Îµ Îµ0 â†¦ ?_
    refine âŸ¨(1, Îµ), âŸ¨one_pos, Îµ0âŸ©, fun (x, y) âŸ¨hx, hyâŸ© => ?_âŸ©
    simp only [sub_zero] at *
    calc norm (x * y) â‰¤ norm x * norm y := norm_mul_le _ _
    _ < Îµ := mul_lt_of_le_one_of_lt_of_nonneg hx.le hy (norm_nonneg _)
  case hmul_left => exact fun x => h0 _ (norm x) (norm_nonneg _) (norm_mul_le x)
  case hmul_right =>
    exact fun y => h0 (Â· * y) (norm y) (norm_nonneg y) fun x =>
      (norm_mul_le x y).trans_eq (mul_comm _ _)

variable {ğ•œ Î± : Type*} [LinearOrderedField ğ•œ] [TopologicalSpace ğ•œ] [OrderTopology ğ•œ]
  {l : Filter Î±} {f g : Î± â†’ ğ•œ}

-- see Note [lower instance priority]
instance (priority := 100) LinearOrderedField.topologicalRing : TopologicalRing ğ•œ :=
  .of_norm abs abs_nonneg (fun _ _ â†¦ (abs_mul _ _).le) <| by
    simpa using nhds_basis_abs_sub_lt (0 : ğ•œ)

/-- In a linearly ordered field with the order topology, if `f` tends to `Filter.atTop` and `g`
tends to a positive constant `C` then `f * g` tends to `Filter.atTop`. -/
theorem Filter.Tendsto.atTop_mul {C : ğ•œ} (hC : 0 < C) (hf : Tendsto f l atTop)
    (hg : Tendsto g l (ğ“ C)) : Tendsto (fun x => f x * g x) l atTop := by
  refine tendsto_atTop_mono' _ ?_ (hf.atTop_mul_const (half_pos hC))
  filter_upwards [hg.eventually (lt_mem_nhds (half_lt_self hC)), hf.eventually_ge_atTop 0]
    with x hg hf using mul_le_mul_of_nonneg_left hg.le hf
#align filter.tendsto.at_top_mul Filter.Tendsto.atTop_mul

/-- In a linearly ordered field with the order topology, if `f` tends to a positive constant `C` and
`g` tends to `Filter.atTop` then `f * g` tends to `Filter.atTop`. -/
theorem Filter.Tendsto.mul_atTop {C : ğ•œ} (hC : 0 < C) (hf : Tendsto f l (ğ“ C))
    (hg : Tendsto g l atTop) : Tendsto (fun x => f x * g x) l atTop := by
  simpa only [mul_comm] using hg.atTop_mul hC hf
#align filter.tendsto.mul_at_top Filter.Tendsto.mul_atTop

/-- In a linearly ordered field with the order topology, if `f` tends to `Filter.atTop` and `g`
tends to a negative constant `C` then `f * g` tends to `Filter.atBot`. -/
theorem Filter.Tendsto.atTop_mul_neg {C : ğ•œ} (hC : C < 0) (hf : Tendsto f l atTop)
    (hg : Tendsto g l (ğ“ C)) : Tendsto (fun x => f x * g x) l atBot := by
  have := hf.atTop_mul (neg_pos.2 hC) hg.neg
  simpa only [(Â· âˆ˜ Â·), neg_mul_eq_mul_neg, neg_neg] using tendsto_neg_atTop_atBot.comp this
#align filter.tendsto.at_top_mul_neg Filter.Tendsto.atTop_mul_neg

/-- In a linearly ordered field with the order topology, if `f` tends to a negative constant `C` and
`g` tends to `Filter.atTop` then `f * g` tends to `Filter.atBot`. -/
theorem Filter.Tendsto.neg_mul_atTop {C : ğ•œ} (hC : C < 0) (hf : Tendsto f l (ğ“ C))
    (hg : Tendsto g l atTop) : Tendsto (fun x => f x * g x) l atBot := by
  simpa only [mul_comm] using hg.atTop_mul_neg hC hf
#align filter.tendsto.neg_mul_at_top Filter.Tendsto.neg_mul_atTop

/-- In a linearly ordered field with the order topology, if `f` tends to `Filter.atBot` and `g`
tends to a positive constant `C` then `f * g` tends to `Filter.atBot`. -/
theorem Filter.Tendsto.atBot_mul {C : ğ•œ} (hC : 0 < C) (hf : Tendsto f l atBot)
    (hg : Tendsto g l (ğ“ C)) : Tendsto (fun x => f x * g x) l atBot := by
  have := (tendsto_neg_atBot_atTop.comp hf).atTop_mul hC hg
  simpa [(Â· âˆ˜ Â·)] using tendsto_neg_atTop_atBot.comp this
#align filter.tendsto.at_bot_mul Filter.Tendsto.atBot_mul

/-- In a linearly ordered field with the order topology, if `f` tends to `Filter.atBot` and `g`
tends to a negative constant `C` then `f * g` tends to `Filter.atTop`. -/
theorem Filter.Tendsto.atBot_mul_neg {C : ğ•œ} (hC : C < 0) (hf : Tendsto f l atBot)
    (hg : Tendsto g l (ğ“ C)) : Tendsto (fun x => f x * g x) l atTop := by
  have := (tendsto_neg_atBot_atTop.comp hf).atTop_mul_neg hC hg
  simpa [(Â· âˆ˜ Â·)] using tendsto_neg_atBot_atTop.comp this
#align filter.tendsto.at_bot_mul_neg Filter.Tendsto.atBot_mul_neg

/-- In a linearly ordered field with the order topology, if `f` tends to a positive constant `C` and
`g` tends to `Filter.atBot` then `f * g` tends to `Filter.atBot`. -/
theorem Filter.Tendsto.mul_atBot {C : ğ•œ} (hC : 0 < C) (hf : Tendsto f l (ğ“ C))
    (hg : Tendsto g l atBot) : Tendsto (fun x => f x * g x) l atBot := by
  simpa only [mul_comm] using hg.atBot_mul hC hf
#align filter.tendsto.mul_at_bot Filter.Tendsto.mul_atBot

/-- In a linearly ordered field with the order topology, if `f` tends to a negative constant `C` and
`g` tends to `Filter.atBot` then `f * g` tends to `Filter.atTop`. -/
theorem Filter.Tendsto.neg_mul_atBot {C : ğ•œ} (hC : C < 0) (hf : Tendsto f l (ğ“ C))
    (hg : Tendsto g l atBot) : Tendsto (fun x => f x * g x) l atTop := by
  simpa only [mul_comm] using hg.atBot_mul_neg hC hf
#align filter.tendsto.neg_mul_at_bot Filter.Tendsto.neg_mul_atBot

@[simp]
lemma inv_atTopâ‚€ : (atTop : Filter ğ•œ)â»Â¹ = ğ“[>] 0 :=
  (((atTop_basis_Ioi' (0 : ğ•œ)).map _).comp_surjective inv_surjective).eq_of_same_basis <|
    (nhdsWithin_Ioi_basis _).congr (by simp) fun a ha â†¦ by simp [inv_Ioi (inv_pos.2 ha)]

@[simp] lemma inv_nhdsWithin_Ioi_zero : (ğ“[>] (0 : ğ•œ))â»Â¹ = atTop := by
  rw [â† inv_atTopâ‚€, inv_inv]

/-- The function `x â†¦ xâ»Â¹` tends to `+âˆ` on the right of `0`. -/
theorem tendsto_inv_zero_atTop : Tendsto (fun x : ğ•œ => xâ»Â¹) (ğ“[>] (0 : ğ•œ)) atTop :=
  inv_nhdsWithin_Ioi_zero.le
#align tendsto_inv_zero_at_top tendsto_inv_zero_atTop

/-- The function `r â†¦ râ»Â¹` tends to `0` on the right as `r â†’ +âˆ`. -/
theorem tendsto_inv_atTop_zero' : Tendsto (fun r : ğ•œ => râ»Â¹) atTop (ğ“[>] (0 : ğ•œ)) :=
  inv_atTopâ‚€.le
#align tendsto_inv_at_top_zero' tendsto_inv_atTop_zero'

theorem tendsto_inv_atTop_zero : Tendsto (fun r : ğ•œ => râ»Â¹) atTop (ğ“ 0) :=
  tendsto_inv_atTop_zero'.mono_right inf_le_left
#align tendsto_inv_at_top_zero tendsto_inv_atTop_zero

theorem Filter.Tendsto.div_atTop {a : ğ•œ} (h : Tendsto f l (ğ“ a)) (hg : Tendsto g l atTop) :
    Tendsto (fun x => f x / g x) l (ğ“ 0) := by
  simp only [div_eq_mul_inv]
  exact mul_zero a â–¸ h.mul (tendsto_inv_atTop_zero.comp hg)
#align filter.tendsto.div_at_top Filter.Tendsto.div_atTop

theorem Filter.Tendsto.inv_tendsto_atTop (h : Tendsto f l atTop) : Tendsto fâ»Â¹ l (ğ“ 0) :=
  tendsto_inv_atTop_zero.comp h
#align filter.tendsto.inv_tendsto_at_top Filter.Tendsto.inv_tendsto_atTop

theorem Filter.Tendsto.inv_tendsto_zero (h : Tendsto f l (ğ“[>] 0)) : Tendsto fâ»Â¹ l atTop :=
  tendsto_inv_zero_atTop.comp h
#align filter.tendsto.inv_tendsto_zero Filter.Tendsto.inv_tendsto_zero

/-- The function `x^(-n)` tends to `0` at `+âˆ` for any positive natural `n`.
A version for positive real powers exists as `tendsto_rpow_neg_atTop`. -/
theorem tendsto_pow_neg_atTop {n : â„•} (hn : n â‰  0) :
    Tendsto (fun x : ğ•œ => x ^ (-(n : â„¤))) atTop (ğ“ 0) := by
  simpa only [zpow_neg, zpow_natCast] using (@tendsto_pow_atTop ğ•œ _ _ hn).inv_tendsto_atTop
#align tendsto_pow_neg_at_top tendsto_pow_neg_atTop

theorem tendsto_zpow_atTop_zero {n : â„¤} (hn : n < 0) :
    Tendsto (fun x : ğ•œ => x ^ n) atTop (ğ“ 0) := by
  lift -n to â„• using le_of_lt (neg_pos.mpr hn) with N h
  rw [â† neg_pos, â† h, Nat.cast_pos] at hn
  simpa only [h, neg_neg] using tendsto_pow_neg_atTop hn.ne'
#align tendsto_zpow_at_top_zero tendsto_zpow_atTop_zero

theorem tendsto_const_mul_zpow_atTop_zero {n : â„¤} {c : ğ•œ} (hn : n < 0) :
    Tendsto (fun x => c * x ^ n) atTop (ğ“ 0) :=
  mul_zero c â–¸ Filter.Tendsto.const_mul c (tendsto_zpow_atTop_zero hn)
#align tendsto_const_mul_zpow_at_top_zero tendsto_const_mul_zpow_atTop_zero

theorem tendsto_const_mul_pow_nhds_iff' {n : â„•} {c d : ğ•œ} :
    Tendsto (fun x : ğ•œ => c * x ^ n) atTop (ğ“ d) â†” (c = 0 âˆ¨ n = 0) âˆ§ c = d := by
  rcases eq_or_ne n 0 with (rfl | hn)
  Â· simp [tendsto_const_nhds_iff]
  rcases lt_trichotomy c 0 with (hc | rfl | hc)
  Â· have := tendsto_const_mul_pow_atBot_iff.2 âŸ¨hn, hcâŸ©
    simp [not_tendsto_nhds_of_tendsto_atBot this, hc.ne, hn]
  Â· simp [tendsto_const_nhds_iff]
  Â· have := tendsto_const_mul_pow_atTop_iff.2 âŸ¨hn, hcâŸ©
    simp [not_tendsto_nhds_of_tendsto_atTop this, hc.ne', hn]
#align tendsto_const_mul_pow_nhds_iff' tendsto_const_mul_pow_nhds_iff'

theorem tendsto_const_mul_pow_nhds_iff {n : â„•} {c d : ğ•œ} (hc : c â‰  0) :
    Tendsto (fun x : ğ•œ => c * x ^ n) atTop (ğ“ d) â†” n = 0 âˆ§ c = d := by
  simp [tendsto_const_mul_pow_nhds_iff', hc]
#align tendsto_const_mul_pow_nhds_iff tendsto_const_mul_pow_nhds_iff

theorem tendsto_const_mul_zpow_atTop_nhds_iff {n : â„¤} {c d : ğ•œ} (hc : c â‰  0) :
    Tendsto (fun x : ğ•œ => c * x ^ n) atTop (ğ“ d) â†” n = 0 âˆ§ c = d âˆ¨ n < 0 âˆ§ d = 0 := by
  refine âŸ¨fun h => ?_, fun h => ?_âŸ©
  Â· cases n with -- Porting note: Lean 3 proof used `by_cases`, then `lift` but `lift` failed
    | ofNat n =>
      left
      simpa [tendsto_const_mul_pow_nhds_iff hc] using h
    | negSucc n =>
      have hn := Int.negSucc_lt_zero n
      exact Or.inr âŸ¨hn, tendsto_nhds_unique h (tendsto_const_mul_zpow_atTop_zero hn)âŸ©
  Â· cases' h with h h
    Â· simp only [h.left, h.right, zpow_zero, mul_one]
      exact tendsto_const_nhds
    Â· exact h.2.symm â–¸ tendsto_const_mul_zpow_atTop_zero h.1
#align tendsto_const_mul_zpow_at_top_nhds_iff tendsto_const_mul_zpow_atTop_nhds_iff

-- see Note [lower instance priority]
instance (priority := 100) LinearOrderedSemifield.toHasContinuousInvâ‚€ {ğ•œ}
    [LinearOrderedSemifield ğ•œ] [TopologicalSpace ğ•œ] [OrderTopology ğ•œ] [ContinuousMul ğ•œ] :
    HasContinuousInvâ‚€ ğ•œ := .of_nhds_one <| tendsto_order.2 <| by
  refine âŸ¨fun x hx => ?_, fun x hx => ?_âŸ©
  Â· obtain âŸ¨x', hâ‚€, hxx', hâ‚âŸ© : âˆƒ x', 0 < x' âˆ§ x â‰¤ x' âˆ§ x' < 1 :=
      âŸ¨max x (1 / 2), one_half_pos.trans_le (le_max_right _ _), le_max_left _ _,
        max_lt hx one_half_lt_oneâŸ©
    filter_upwards [Ioo_mem_nhds one_pos (one_lt_inv hâ‚€ hâ‚)] with y hy
    exact hxx'.trans_lt <| inv_inv x' â–¸ inv_lt_inv_of_lt hy.1 hy.2
  Â· filter_upwards [Ioi_mem_nhds (inv_lt_one hx)] with y hy
    simpa only [inv_inv] using inv_lt_inv_of_lt (inv_pos.2 <| one_pos.trans hx) hy

instance (priority := 100) LinearOrderedField.toTopologicalDivisionRing :
    TopologicalDivisionRing ğ•œ := âŸ¨âŸ©
#align linear_ordered_field.to_topological_division_ring LinearOrderedField.toTopologicalDivisionRing

-- Porting note (#11215): TODO: generalize to a `GroupWithZero`
theorem nhdsWithin_pos_comap_mul_left {x : ğ•œ} (hx : 0 < x) :
    comap (x * Â·) (ğ“[>] 0) = ğ“[>] 0 := by
  rw [nhdsWithin, comap_inf, comap_principal, preimage_const_mul_Ioi _ hx, zero_div]
  congr 1
  refine ((Homeomorph.mulLeftâ‚€ x hx.ne').comap_nhds_eq _).trans ?_
  simp
#align nhds_within_pos_comap_mul_left nhdsWithin_pos_comap_mul_left

theorem eventually_nhdsWithin_pos_mul_left {x : ğ•œ} (hx : 0 < x) {p : ğ•œ â†’ Prop}
    (h : âˆ€á¶  Îµ in ğ“[>] 0, p Îµ) : âˆ€á¶  Îµ in ğ“[>] 0, p (x * Îµ) := by
  rw [â† nhdsWithin_pos_comap_mul_left hx]
  exact h.comap fun Îµ => x * Îµ
#align eventually_nhds_within_pos_mul_left eventually_nhdsWithin_pos_mul_left
