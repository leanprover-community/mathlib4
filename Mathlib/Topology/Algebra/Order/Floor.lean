/-
Copyright (c) 2020 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import Mathlib.Algebra.Order.Floor.Ring
import Mathlib.Order.Filter.AtTopBot.Floor
import Mathlib.Topology.Algebra.Order.Group

/-!
# Topological facts about `Int.floor`, `Int.ceil` and `Int.fract`

This file proves statements about limits and continuity of functions involving `floor`, `ceil` and
`fract`.

## Main declarations

* `tendsto_floor_atTop`, `tendsto_floor_atBot`, `tendsto_ceil_atTop`, `tendsto_ceil_atBot`:
  `Int.floor` and `Int.ceil` tend to +-âˆ in +-âˆ.
* `continuousOn_floor`: `Int.floor` is continuous on `Ico n (n + 1)`, because constant.
* `continuousOn_ceil`: `Int.ceil` is continuous on `Ioc n (n + 1)`, because constant.
* `continuousOn_fract`: `Int.fract` is continuous on `Ico n (n + 1)`.
* `ContinuousOn.comp_fract`: Precomposing a continuous function satisfying `f 0 = f 1` with
  `Int.fract` yields another continuous function.
-/


open Filter Function Int Set Topology

namespace FloorSemiring

open scoped Nat

variable {K : Type*} [Field K] [LinearOrder K] [IsStrictOrderedRing K] [FloorSemiring K]
  [TopologicalSpace K] [OrderTopology K]

theorem tendsto_mul_pow_div_factorial_sub_atTop (a c : K) (d : â„•) :
    Tendsto (fun n â†¦ a * c ^ n / (n - d)!) atTop (ğ“ 0) := by
  rw [tendsto_order]
  constructor
  all_goals
    intro Îµ hÎµ
    filter_upwards [eventually_mul_pow_lt_factorial_sub (a * Îµâ»Â¹) c d] with n h
    rw [mul_right_comm, â† div_eq_mul_inv] at h
  Â· rw [div_lt_iff_of_neg hÎµ] at h
    rwa [lt_div_iffâ‚€' (Nat.cast_pos.mpr (Nat.factorial_pos _))]
  Â· rw [div_lt_iffâ‚€ hÎµ] at h
    rwa [div_lt_iffâ‚€' (Nat.cast_pos.mpr (Nat.factorial_pos _))]

theorem tendsto_pow_div_factorial_atTop (c : K) :
    Tendsto (fun n â†¦ c ^ n / n !) atTop (ğ“ 0) := by
  convert tendsto_mul_pow_div_factorial_sub_atTop 1 c 0
  rw [one_mul]

end FloorSemiring

variable {Î± Î² Î³ : Type*} [Ring Î±] [LinearOrder Î±] [FloorRing Î±]

section
variable [IsStrictOrderedRing Î±]
-- TODO: move to `Mathlib/Order/Filter/AtTopBot/Floor.lean`

theorem tendsto_floor_atTop : Tendsto (floor : Î± â†’ â„¤) atTop atTop :=
  floor_mono.tendsto_atTop_atTop fun b =>
    âŸ¨(b + 1 : â„¤), by rw [floor_intCast]; exact (lt_add_one _).leâŸ©

theorem tendsto_floor_atBot : Tendsto (floor : Î± â†’ â„¤) atBot atBot :=
  floor_mono.tendsto_atBot_atBot fun b => âŸ¨b, (floor_intCast _).leâŸ©

theorem tendsto_ceil_atTop : Tendsto (ceil : Î± â†’ â„¤) atTop atTop :=
  ceil_mono.tendsto_atTop_atTop fun b => âŸ¨b, (ceil_intCast _).geâŸ©

theorem tendsto_ceil_atBot : Tendsto (ceil : Î± â†’ â„¤) atBot atBot :=
  ceil_mono.tendsto_atBot_atBot fun b =>
    âŸ¨(b - 1 : â„¤), by rw [ceil_intCast]; exact (sub_one_lt _).leâŸ©

end

variable [TopologicalSpace Î±]

theorem continuousOn_floor (n : â„¤) :
    ContinuousOn (fun x => floor x : Î± â†’ Î±) (Ico n (n + 1) : Set Î±) :=
  (continuousOn_congr <| floor_eq_on_Ico' n).mpr continuousOn_const

theorem continuousOn_ceil [IsStrictOrderedRing Î±] (n : â„¤) :
    ContinuousOn (fun x => ceil x : Î± â†’ Î±) (Ioc (n - 1) n : Set Î±) :=
  (continuousOn_congr <| ceil_eq_on_Ioc' n).mpr continuousOn_const

section OrderClosedTopology

variable [IsStrictOrderedRing Î±] [OrderClosedTopology Î±]

omit [IsStrictOrderedRing Î±] in
theorem tendsto_floor_right_pure_floor (x : Î±) : Tendsto (floor : Î± â†’ â„¤) (ğ“[â‰¥] x) (pure âŒŠxâŒ‹) :=
  tendsto_pure.2 <| mem_of_superset (Ico_mem_nhdsGE <| lt_floor_add_one x) fun _y hy =>
    floor_eq_on_Ico _ _ âŸ¨(floor_le x).trans hy.1, hy.2âŸ©

theorem tendsto_floor_right_pure (n : â„¤) : Tendsto (floor : Î± â†’ â„¤) (ğ“[â‰¥] n) (pure n) := by
  simpa only [floor_intCast] using tendsto_floor_right_pure_floor (n : Î±)

theorem tendsto_ceil_left_pure_ceil (x : Î±) : Tendsto (ceil : Î± â†’ â„¤) (ğ“[â‰¤] x) (pure âŒˆxâŒ‰) :=
  tendsto_pure.2 <| mem_of_superset
    (Ioc_mem_nhdsLE <| sub_lt_iff_lt_add.2 <| ceil_lt_add_one _) fun _y hy =>
      ceil_eq_on_Ioc _ _ âŸ¨hy.1, hy.2.trans (le_ceil _)âŸ©

theorem tendsto_ceil_left_pure (n : â„¤) : Tendsto (ceil : Î± â†’ â„¤) (ğ“[â‰¤] n) (pure n) := by
  simpa only [ceil_intCast] using tendsto_ceil_left_pure_ceil (n : Î±)

theorem tendsto_floor_left_pure_ceil_sub_one (x : Î±) :
    Tendsto (floor : Î± â†’ â„¤) (ğ“[<] x) (pure (âŒˆxâŒ‰ - 1)) :=
  have hâ‚ : â†‘(âŒˆxâŒ‰ - 1) < x := by rw [cast_sub, cast_one, sub_lt_iff_lt_add]; exact ceil_lt_add_one _
  have hâ‚‚ : x â‰¤ â†‘(âŒˆxâŒ‰ - 1) + 1 := by rw [cast_sub, cast_one, sub_add_cancel]; exact le_ceil _
  tendsto_pure.2 <| mem_of_superset (Ico_mem_nhdsLT hâ‚) fun _y hy =>
    floor_eq_on_Ico _ _ âŸ¨hy.1, hy.2.trans_le hâ‚‚âŸ©

theorem tendsto_floor_left_pure_sub_one (n : â„¤) :
    Tendsto (floor : Î± â†’ â„¤) (ğ“[<] n) (pure (n - 1)) := by
  simpa only [ceil_intCast] using tendsto_floor_left_pure_ceil_sub_one (n : Î±)

omit [IsStrictOrderedRing Î±] in
theorem tendsto_ceil_right_pure_floor_add_one (x : Î±) :
    Tendsto (ceil : Î± â†’ â„¤) (ğ“[>] x) (pure (âŒŠxâŒ‹ + 1)) :=
  have : â†‘(âŒŠxâŒ‹ + 1) - 1 â‰¤ x := by rw [cast_add, cast_one, add_sub_cancel_right]; exact floor_le _
  tendsto_pure.2 <| mem_of_superset (Ioc_mem_nhdsGT <| lt_succ_floor _) fun _y hy =>
    ceil_eq_on_Ioc _ _ âŸ¨this.trans_lt hy.1, hy.2âŸ©

theorem tendsto_ceil_right_pure_add_one (n : â„¤) :
    Tendsto (ceil : Î± â†’ â„¤) (ğ“[>] n) (pure (n + 1)) := by
  simpa only [floor_intCast] using tendsto_ceil_right_pure_floor_add_one (n : Î±)

theorem tendsto_floor_right (n : â„¤) : Tendsto (fun x => floor x : Î± â†’ Î±) (ğ“[â‰¥] n) (ğ“[â‰¥] n) :=
  ((tendsto_pure_pure _ _).comp (tendsto_floor_right_pure n)).mono_right <|
    pure_le_nhdsWithin le_rfl

theorem tendsto_floor_right' (n : â„¤) : Tendsto (fun x => floor x : Î± â†’ Î±) (ğ“[â‰¥] n) (ğ“ n) :=
  (tendsto_floor_right n).mono_right inf_le_left

theorem tendsto_ceil_left (n : â„¤) : Tendsto (fun x => ceil x : Î± â†’ Î±) (ğ“[â‰¤] n) (ğ“[â‰¤] n) :=
  ((tendsto_pure_pure _ _).comp (tendsto_ceil_left_pure n)).mono_right <|
    pure_le_nhdsWithin le_rfl

theorem tendsto_ceil_left' (n : â„¤) :
    Tendsto (fun x => ceil x : Î± â†’ Î±) (ğ“[â‰¤] n) (ğ“ n) :=
  (tendsto_ceil_left n).mono_right inf_le_left

theorem tendsto_floor_left (n : â„¤) :
    Tendsto (fun x => floor x : Î± â†’ Î±) (ğ“[<] n) (ğ“[â‰¤] (n - 1)) :=
  ((tendsto_pure_pure _ _).comp (tendsto_floor_left_pure_sub_one n)).mono_right <| by
    rw [â† @cast_one Î±, â† cast_sub]; exact pure_le_nhdsWithin le_rfl

theorem tendsto_ceil_right (n : â„¤) :
    Tendsto (fun x => ceil x : Î± â†’ Î±) (ğ“[>] n) (ğ“[â‰¥] (n + 1)) :=
  ((tendsto_pure_pure _ _).comp (tendsto_ceil_right_pure_add_one n)).mono_right <| by
    rw [â† @cast_one Î±, â† cast_add]; exact pure_le_nhdsWithin le_rfl

theorem tendsto_floor_left' (n : â„¤) :
    Tendsto (fun x => floor x : Î± â†’ Î±) (ğ“[<] n) (ğ“ (n - 1)) :=
  (tendsto_floor_left n).mono_right inf_le_left

theorem tendsto_ceil_right' (n : â„¤) :
    Tendsto (fun x => ceil x : Î± â†’ Î±) (ğ“[>] n) (ğ“ (n + 1)) :=
  (tendsto_ceil_right n).mono_right inf_le_left

end OrderClosedTopology

theorem continuousOn_fract [IsTopologicalAddGroup Î±] (n : â„¤) :
    ContinuousOn (fract : Î± â†’ Î±) (Ico n (n + 1) : Set Î±) :=
  continuousOn_id.sub (continuousOn_floor n)

theorem continuousAt_fract [OrderClosedTopology Î±] [IsTopologicalAddGroup Î±]
    {x : Î±} (h : x â‰  âŒŠxâŒ‹) : ContinuousAt fract x :=
  (continuousOn_fract âŒŠxâŒ‹).continuousAt <|
    Ico_mem_nhds ((floor_le _).lt_of_ne h.symm) (lt_floor_add_one _)

variable [IsStrictOrderedRing Î±]

theorem tendsto_fract_left' [OrderClosedTopology Î±] [IsTopologicalAddGroup Î±] (n : â„¤) :
    Tendsto (fract : Î± â†’ Î±) (ğ“[<] n) (ğ“ 1) := by
  rw [â† sub_sub_cancel (n : Î±) 1]
  refine (tendsto_id.mono_left nhdsWithin_le_nhds).sub ?_
  exact tendsto_floor_left' n

theorem tendsto_fract_left [OrderClosedTopology Î±] [IsTopologicalAddGroup Î±] (n : â„¤) :
    Tendsto (fract : Î± â†’ Î±) (ğ“[<] n) (ğ“[<] 1) :=
  tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within _ (tendsto_fract_left' _)
    (Eventually.of_forall fract_lt_one)

theorem tendsto_fract_right' [OrderClosedTopology Î±] [IsTopologicalAddGroup Î±] (n : â„¤) :
    Tendsto (fract : Î± â†’ Î±) (ğ“[â‰¥] n) (ğ“ 0) :=
  sub_self (n : Î±) â–¸ (tendsto_nhdsWithin_of_tendsto_nhds tendsto_id).sub (tendsto_floor_right' n)

theorem tendsto_fract_right [OrderClosedTopology Î±] [IsTopologicalAddGroup Î±] (n : â„¤) :
    Tendsto (fract : Î± â†’ Î±) (ğ“[â‰¥] n) (ğ“[â‰¥] 0) :=
  tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within _ (tendsto_fract_right' _)
    (Eventually.of_forall fract_nonneg)

local notation "I" => (Icc 0 1 : Set Î±)

variable [OrderTopology Î±] [TopologicalSpace Î²] [TopologicalSpace Î³]

/-- Do not use this, use `ContinuousOn.comp_fract` instead. -/
theorem ContinuousOn.comp_fract' {f : Î² â†’ Î± â†’ Î³} (h : ContinuousOn (uncurry f) <| univ Ã—Ë¢ I)
    (hf : âˆ€ s, f s 0 = f s 1) : Continuous fun st : Î² Ã— Î± => f st.1 (fract st.2) := by
  change Continuous (uncurry f âˆ˜ Prod.map id fract)
  rw [continuous_iff_continuousAt]
  rintro âŸ¨s, tâŸ©
  rcases em (âˆƒ n : â„¤, t = n) with (âŸ¨n, rflâŸ© | ht)
  Â· rw [ContinuousAt, nhds_prod_eq, â† nhdsLT_sup_nhdsGE (n : Î±), prod_sup, tendsto_sup]
    constructor
    Â· refine (((h (s, 1) âŸ¨trivial, zero_le_one, le_rflâŸ©).tendsto.mono_left ?_).comp
        (tendsto_id.prodMap (tendsto_fract_left _))).mono_right (le_of_eq ?_)
      Â· rw [nhdsWithin_prod_eq, nhdsWithin_univ, â† nhdsWithin_Ico_eq_nhdsLT one_pos]
        exact Filter.prod_mono le_rfl (nhdsWithin_mono _ Ico_subset_Icc_self)
      Â· simp [hf]
    Â· refine (((h (s, 0) âŸ¨trivial, le_rfl, zero_le_oneâŸ©).tendsto.mono_left <| le_of_eq ?_).comp
        (tendsto_id.prodMap (tendsto_fract_right _))).mono_right (le_of_eq ?_) <;>
        simp [nhdsWithin_prod_eq, nhdsWithin_univ]
  Â· replace ht : t â‰  âŒŠtâŒ‹ := fun ht' => ht âŸ¨_, ht'âŸ©
    refine (h.continuousAt ?_).comp (continuousAt_id.prodMap (continuousAt_fract ht))
    exact prod_mem_nhds univ_mem (Icc_mem_nhds (fract_pos.2 ht) (fract_lt_one _))

theorem ContinuousOn.comp_fract {s : Î² â†’ Î±} {f : Î² â†’ Î± â†’ Î³}
    (h : ContinuousOn (uncurry f) <| univ Ã—Ë¢ Icc 0 1) (hs : Continuous s)
    (hf : âˆ€ s, f s 0 = f s 1) : Continuous fun x : Î² => f x <| Int.fract (s x) :=
  (h.comp_fract' hf).comp (continuous_id.prodMk hs)

/-- A special case of `ContinuousOn.comp_fract`. -/
theorem ContinuousOn.comp_fract'' {f : Î± â†’ Î²} (h : ContinuousOn f I) (hf : f 0 = f 1) :
    Continuous (f âˆ˜ fract) :=
  ContinuousOn.comp_fract (h.comp continuousOn_snd fun _x hx => (mem_prod.mp hx).2) continuous_id
    fun _ => hf
