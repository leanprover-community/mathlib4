/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Topology.Order.Basic
import Mathlib.Topology.Algebra.Group.Basic

#align_import topology.algebra.order.group from "leanprover-community/mathlib"@"84dc0bd6619acaea625086d6f53cb35cdd554219"

/-!
# Topology on a linear ordered additive commutative group

In this file we prove that a linear ordered additive commutative group with order topology is a
topological group. We also prove continuity of `abs : G â†’ G` and provide convenience lemmas like
`ContinuousAt.abs`.
-/


open Set Filter

open Topology Filter

variable {Î± G : Type*} [TopologicalSpace G] [LinearOrderedAddCommGroup G] [OrderTopology G]

variable {l : Filter Î±} {f g : Î± â†’ G}

-- see Note [lower instance priority]
instance (priority := 100) LinearOrderedAddCommGroup.topologicalAddGroup : TopologicalAddGroup G
    where
  continuous_add := by
    refine' continuous_iff_continuousAt.2 _
    -- âŠ¢ âˆ€ (x : G Ã— G), ContinuousAt (fun p => p.fst + p.snd) x
    rintro âŸ¨a, bâŸ©
    -- âŠ¢ ContinuousAt (fun p => p.fst + p.snd) (a, b)
    refine' LinearOrderedAddCommGroup.tendsto_nhds.2 fun Îµ Îµ0 => _
    -- âŠ¢ âˆ€á¶  (b_1 : G Ã— G) in ð“ (a, b), |b_1.fst + b_1.snd - (fun p => p.fst + p.snd)  â€¦
    rcases dense_or_discrete 0 Îµ with (âŸ¨Î´, Î´0, Î´ÎµâŸ© | âŸ¨_hâ‚, hâ‚‚âŸ©)
    -- âŠ¢ âˆ€á¶  (b_1 : G Ã— G) in ð“ (a, b), |b_1.fst + b_1.snd - (fun p => p.fst + p.snd)  â€¦
    Â· -- If there exists `Î´ âˆˆ (0, Îµ)`, then we choose `Î´`-nhd of `a` and `(Îµ-Î´)`-nhd of `b`
      filter_upwards [(eventually_abs_sub_lt a Î´0).prod_nhds
          (eventually_abs_sub_lt b (sub_pos.2 Î´Îµ))]
      rintro âŸ¨x, yâŸ© âŸ¨hx : |x - a| < Î´, hy : |y - b| < Îµ - Î´âŸ©
      -- âŠ¢ |(x, y).fst + (x, y).snd - (a + b)| < Îµ
      rw [add_sub_add_comm]
      -- âŠ¢ |(x, y).fst - a + ((x, y).snd - b)| < Îµ
      calc
        |x - a + (y - b)| â‰¤ |x - a| + |y - b| := abs_add _ _
        _ < Î´ + (Îµ - Î´) := add_lt_add hx hy
        _ = Îµ := add_sub_cancel'_right _ _
    Â· -- Otherwise `Îµ`-nhd of each point `a` is `{a}`
      have hÎµ : âˆ€ {x y}, |x - y| < Îµ â†’ x = y := by
        intro x y h
        simpa [sub_eq_zero] using hâ‚‚ _ h
      filter_upwards [(eventually_abs_sub_lt a Îµ0).prod_nhds (eventually_abs_sub_lt b Îµ0)]
      -- âŠ¢ âˆ€ (a_1 : G Ã— G), |a_1.fst - a| < Îµ âˆ§ |a_1.snd - b| < Îµ â†’ |a_1.fst + a_1.snd  â€¦
      rintro âŸ¨x, yâŸ© âŸ¨hx : |x - a| < Îµ, hy : |y - b| < ÎµâŸ©
      -- âŠ¢ |(x, y).fst + (x, y).snd - (a + b)| < Îµ
      simpa [hÎµ hx, hÎµ hy]
      -- ðŸŽ‰ no goals
  continuous_neg :=
    continuous_iff_continuousAt.2 fun a =>
      LinearOrderedAddCommGroup.tendsto_nhds.2 fun Îµ Îµ0 =>
        (eventually_abs_sub_lt a Îµ0).mono fun x hx => by rwa [neg_sub_neg, abs_sub_comm]
                                                         -- ðŸŽ‰ no goals
#align linear_ordered_add_comm_group.topological_add_group LinearOrderedAddCommGroup.topologicalAddGroup

@[continuity]
theorem continuous_abs : Continuous (abs : G â†’ G) :=
  continuous_id.max continuous_neg
#align continuous_abs continuous_abs

protected theorem Filter.Tendsto.abs {a : G} (h : Tendsto f l (ð“ a)) :
    Tendsto (fun x => |f x|) l (ð“ |a|) :=
  (continuous_abs.tendsto _).comp h
#align filter.tendsto.abs Filter.Tendsto.abs

theorem tendsto_zero_iff_abs_tendsto_zero (f : Î± â†’ G) :
    Tendsto f l (ð“ 0) â†” Tendsto (abs âˆ˜ f) l (ð“ 0) := by
  refine' âŸ¨fun h => (abs_zero : |(0 : G)| = 0) â–¸ h.abs, fun h => _âŸ©
  -- âŠ¢ Tendsto f l (ð“ 0)
  have : Tendsto (fun a => -|f a|) l (ð“ 0) := (neg_zero : -(0 : G) = 0) â–¸ h.neg
  -- âŠ¢ Tendsto f l (ð“ 0)
  exact
    tendsto_of_tendsto_of_tendsto_of_le_of_le this h (fun x => neg_abs_le_self <| f x) fun x =>
      le_abs_self <| f x
#align tendsto_zero_iff_abs_tendsto_zero tendsto_zero_iff_abs_tendsto_zero

variable [TopologicalSpace Î±] {a : Î±} {s : Set Î±}

protected theorem Continuous.abs (h : Continuous f) : Continuous fun x => |f x| :=
  continuous_abs.comp h
#align continuous.abs Continuous.abs

protected theorem ContinuousAt.abs (h : ContinuousAt f a) : ContinuousAt (fun x => |f x|) a :=
  Filter.Tendsto.abs h
#align continuous_at.abs ContinuousAt.abs

protected theorem ContinuousWithinAt.abs (h : ContinuousWithinAt f s a) :
    ContinuousWithinAt (fun x => |f x|) s a :=
  Filter.Tendsto.abs h
#align continuous_within_at.abs ContinuousWithinAt.abs

protected theorem ContinuousOn.abs (h : ContinuousOn f s) : ContinuousOn (fun x => |f x|) s :=
  fun x hx => (h x hx).abs
#align continuous_on.abs ContinuousOn.abs

theorem tendsto_abs_nhdsWithin_zero : Tendsto (abs : G â†’ G) (ð“[â‰ ] 0) (ð“[>] 0) :=
  (continuous_abs.tendsto' (0 : G) 0 abs_zero).inf <|
    tendsto_principal_principal.2 fun _x => abs_pos.2
#align tendsto_abs_nhds_within_zero tendsto_abs_nhdsWithin_zero
