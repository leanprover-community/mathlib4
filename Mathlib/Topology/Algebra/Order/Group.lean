/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Algebra.Order.Group.Basic
import Mathlib.Topology.Algebra.Group.Defs
import Mathlib.Topology.Order.LeftRightNhds

/-!
# Topology on a linear ordered commutative group

In this file we prove that a linear ordered commutative group with order topology
is a topological group.
We also prove continuity of `abs : G ‚Üí G` and provide convenience lemmas like `ContinuousAt.abs`.
-/


open Set Filter Function

open scoped Topology

variable {G : Type*} [TopologicalSpace G] [CommGroup G] [LinearOrder G] [IsOrderedMonoid G]
  [OrderTopology G]

-- see Note [lower instance priority]
@[to_additive]
instance (priority := 100) LinearOrderedCommGroup.toIsTopologicalGroup :
    IsTopologicalGroup G where
  continuous_mul := by
    simp only [continuous_iff_continuousAt, Prod.forall, ContinuousAt,
      LinearOrderedCommGroup.tendsto_nhds]
    intro a b Œµ hŒµ
    rcases dense_or_discrete 1 Œµ with ‚ü®Œ¥, hŒ¥‚ÇÅ, hŒ¥Œµ‚ü© | ‚ü®-, hŒµ_min‚ü©
    ¬∑ filter_upwards [(eventually_mabs_div_lt _ hŒ¥‚ÇÅ).prod_nhds
        (eventually_mabs_div_lt _ (one_lt_div'.mpr hŒ¥Œµ))]
      rintro ‚ü®c, d‚ü© ‚ü®hc, hd‚ü©
      calc
        |c * d / (a * b)|‚Çò = |(c / a) * (d / b)|‚Çò := by rw [div_mul_div_comm]
        _ ‚â§ |c / a|‚Çò * |d / b|‚Çò := mabs_mul_le ..
        _ < Œ¥ * (Œµ / Œ¥) := mul_lt_mul_of_lt_of_lt hc hd
        _ = Œµ := mul_div_cancel ..
    ¬∑ have (x : G) : ‚àÄ·∂† y in ùìù x, y = x :=
        (eventually_mabs_div_lt _ hŒµ).mono fun y hy ‚Ü¶ mabs_div_le_one.mp <| hŒµ_min _ hy
      filter_upwards [(this _).prod_nhds (this _)]
      simp [hŒµ]
  continuous_inv := continuous_iff_continuousAt.2 fun a ‚Ü¶
    LinearOrderedCommGroup.tendsto_nhds.2 fun Œµ Œµ0 ‚Ü¶
      (eventually_mabs_div_lt a Œµ0).mono fun x hx ‚Ü¶ by rwa [inv_div_inv, mabs_div_comm]

@[to_additive (attr := continuity)]
theorem continuous_mabs : Continuous (mabs : G ‚Üí G) :=
  continuous_id.max continuous_inv

section Tendsto

variable {Œ± : Type*} {l : Filter Œ±} {f : Œ± ‚Üí G}

@[to_additive]
protected theorem Filter.Tendsto.mabs {a : G} (h : Tendsto f l (ùìù a)) :
    Tendsto (fun x => |f x|‚Çò) l (ùìù |a|‚Çò) :=
  (continuous_mabs.tendsto _).comp h

@[to_additive (attr := simp)]
theorem comap_mabs_nhds_one : comap mabs (ùìù (1 : G)) = ùìù 1 := by
  simp [nhds_eq_iInf_mabs_div]

@[to_additive]
theorem tendsto_one_iff_mabs_tendsto_one (f : Œ± ‚Üí G) :
    Tendsto f l (ùìù 1) ‚Üî Tendsto (mabs ‚àò f) l (ùìù 1) := by
  rw [‚Üê tendsto_comap_iff, comap_mabs_nhds_one]

end Tendsto

variable {X : Type*} [TopologicalSpace X] {f : X ‚Üí G} {s : Set X} {x : X}

@[to_additive (attr := fun_prop)]
protected theorem Continuous.mabs (h : Continuous f) : Continuous fun x => |f x|‚Çò :=
  continuous_mabs.comp h

@[to_additive (attr := fun_prop)]
protected theorem ContinuousAt.mabs (h : ContinuousAt f x) : ContinuousAt (fun x => |f x|‚Çò) x :=
  Filter.Tendsto.mabs h

@[to_additive]
protected theorem ContinuousWithinAt.mabs (h : ContinuousWithinAt f s x) :
    ContinuousWithinAt (fun x => |f x|‚Çò) s x :=
  Filter.Tendsto.mabs h

@[to_additive (attr := fun_prop)]
protected theorem ContinuousOn.mabs (h : ContinuousOn f s) : ContinuousOn (fun x => |f x|‚Çò) s :=
  fun x hx => (h x hx).mabs

@[to_additive]
theorem tendsto_mabs_nhdsNE_one : Tendsto (mabs : G ‚Üí G) (ùìù[‚â†] 1) (ùìù[>] 1) :=
  (continuous_mabs.tendsto' (1 : G) 1 mabs_one).inf <|
    tendsto_principal_principal.2 fun _x => one_lt_mabs.2

@[deprecated (since := "2025-03-18")]
alias tendsto_abs_nhdsWithin_zero := tendsto_abs_nhdsNE_zero

/-- In a linearly ordered multiplicative group, the integer powers of an element are dense
iff they are the whole group. -/
@[to_additive /-- In a linearly ordered additive group, the integer multiples of an element are
dense iff they are the whole group. -/]
theorem denseRange_zpow_iff_surjective {a : G} :
    DenseRange (a ^ ¬∑ : ‚Ñ§ ‚Üí G) ‚Üî Surjective (a ^ ¬∑ : ‚Ñ§ ‚Üí G) := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ h.denseRange‚ü©
  wlog ha‚ÇÄ : 1 < a generalizing a
  ¬∑ simp only [‚Üê range_eq_univ, DenseRange] at *
    rcases (not_lt.1 ha‚ÇÄ).eq_or_lt with rfl | hlt
    ¬∑ simpa only [one_zpow, range_const, dense_iff_closure_eq, closure_singleton] using h
    ¬∑ have H : range (a‚Åª¬π ^ ¬∑ : ‚Ñ§ ‚Üí G) = range (a ^ ¬∑ : ‚Ñ§ ‚Üí G) := by
        simpa only [‚Üê inv_zpow, zpow_neg, comp_def] using neg_surjective.range_comp (a ^ ¬∑ : ‚Ñ§ ‚Üí G)
      rw [‚Üê H]
      apply this <;> simpa only [H, one_lt_inv']
  intro b
  obtain ‚ü®m, hm, hm'‚ü© : ‚àÉ m : ‚Ñ§, a ^ m ‚àà Ioo b (b * a * a) := by
    have hne : (Ioo b (b * a * a)).Nonempty := ‚ü®b * a, by simpa‚ü©
    simpa using h.exists_mem_open isOpen_Ioo hne
  rcases eq_or_ne b (a ^ (m - 1)) with rfl | hne; ¬∑ simp
  suffices (Ioo (a ^ m) (a ^ (m + 1))).Nonempty by
    rcases h.exists_mem_open isOpen_Ioo this with ‚ü®l, hl‚ü©
    have : m < l ‚àß l < m + 1 := by simpa [zpow_lt_zpow_iff_right ha‚ÇÄ] using hl
    omega
  rcases hne.lt_or_gt with hlt | hlt
  ¬∑ refine ‚ü®b * a * a, hm', ?_‚ü©
    simpa only [zpow_add, zpow_sub, zpow_one, ‚Üê div_eq_mul_inv, lt_div_iff_mul_lt,
      mul_lt_mul_iff_right] using hlt
  ¬∑ use b * a
    simp only [mem_Ioo, zpow_add, zpow_sub, zpow_one, ‚Üê div_eq_mul_inv,
      mul_lt_mul_iff_right] at hlt ‚ä¢
    exact ‚ü®div_lt_iff_lt_mul.1 hlt, hm‚ü©

/-- In a nontrivial densely linearly ordered commutative group,
the integer powers of an element can't be dense. -/
@[to_additive /-- In a nontrivial densely linearly ordered additive group,
the integer multiples of an element can't be dense. -/]
theorem not_denseRange_zpow [Nontrivial G] [DenselyOrdered G] {a : G} :
    ¬¨DenseRange (a ^ ¬∑ : ‚Ñ§ ‚Üí G) :=
  denseRange_zpow_iff_surjective.not.mpr fun h ‚Ü¶
    not_isCyclic_of_denselyOrdered G ‚ü®‚ü®a, h‚ü©‚ü©
