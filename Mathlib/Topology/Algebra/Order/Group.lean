/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Topology.Algebra.Group.Basic
import Mathlib.Topology.Order.LeftRightNhds

/-!
# Topology on a linear ordered additive commutative group

In this file we prove that a linear ordered additive commutative group with order topology is a
topological group. We also prove continuity of `abs : G â†’ G` and provide convenience lemmas like
`ContinuousAt.abs`.
-/


open Set Filter Function

open scoped Topology

variable {Î± G : Type*} [TopologicalSpace G] [LinearOrderedAddCommGroup G] [OrderTopology G]
variable {l : Filter Î±} {f g : Î± â†’ G}

-- see Note [lower instance priority]
instance (priority := 100) LinearOrderedAddCommGroup.topologicalAddGroup :
    TopologicalAddGroup G where
  continuous_add := by
    refine continuous_iff_continuousAt.2 ?_
    rintro âŸ¨a, bâŸ©
    refine LinearOrderedAddCommGroup.tendsto_nhds.2 fun Îµ Îµ0 => ?_
    rcases dense_or_discrete 0 Îµ with (âŸ¨Î´, Î´0, Î´ÎµâŸ© | âŸ¨_hâ‚, hâ‚‚âŸ©)
    Â· -- If there exists `Î´ âˆˆ (0, Îµ)`, then we choose `Î´`-nhd of `a` and `(Îµ-Î´)`-nhd of `b`
      filter_upwards [(eventually_abs_sub_lt a Î´0).prod_nhds
          (eventually_abs_sub_lt b (sub_pos.2 Î´Îµ))]
      rintro âŸ¨x, yâŸ© âŸ¨hx : |x - a| < Î´, hy : |y - b| < Îµ - Î´âŸ©
      rw [add_sub_add_comm]
      calc
        |x - a + (y - b)| â‰¤ |x - a| + |y - b| := abs_add _ _
        _ < Î´ + (Îµ - Î´) := add_lt_add hx hy
        _ = Îµ := add_sub_cancel _ _
    Â· -- Otherwise `Îµ`-nhd of each point `a` is `{a}`
      have hÎµ : âˆ€ {x y}, |x - y| < Îµ â†’ x = y := by
        intro x y h
        simpa [sub_eq_zero] using hâ‚‚ _ h
      filter_upwards [(eventually_abs_sub_lt a Îµ0).prod_nhds (eventually_abs_sub_lt b Îµ0)]
      rintro âŸ¨x, yâŸ© âŸ¨hx : |x - a| < Îµ, hy : |y - b| < ÎµâŸ©
      simpa [hÎµ hx, hÎµ hy]
  continuous_neg :=
    continuous_iff_continuousAt.2 fun a =>
      LinearOrderedAddCommGroup.tendsto_nhds.2 fun Îµ Îµ0 =>
        (eventually_abs_sub_lt a Îµ0).mono fun x hx => by rwa [neg_sub_neg, abs_sub_comm]

@[continuity]
theorem continuous_abs : Continuous (abs : G â†’ G) :=
  continuous_id.max continuous_neg

protected theorem Filter.Tendsto.abs {a : G} (h : Tendsto f l (ğ“ a)) :
    Tendsto (fun x â†¦ |f x|) l (ğ“ |a|) :=
  (continuous_abs.tendsto _).comp h

theorem tendsto_zero_iff_abs_tendsto_zero (f : Î± â†’ G) :
    Tendsto f l (ğ“ 0) â†” Tendsto (abs âˆ˜ f) l (ğ“ 0) := by
  refine âŸ¨fun h â†¦ (abs_zero : |(0 : G)| = 0) â–¸ h.abs, fun h â†¦ ?_âŸ©
  have : Tendsto (fun a â†¦ -|f a|) l (ğ“ 0) := (neg_zero : -(0 : G) = 0) â–¸ h.neg
  exact
    tendsto_of_tendsto_of_tendsto_of_le_of_le this h (fun x â†¦ neg_abs_le <| f x) fun x =>
      le_abs_self <| f x

variable [TopologicalSpace Î±] {a : Î±} {s : Set Î±}

@[fun_prop]
protected theorem Continuous.abs (h : Continuous f) : Continuous fun x â†¦ |f x| :=
  continuous_abs.comp h

@[fun_prop]
protected theorem ContinuousAt.abs (h : ContinuousAt f a) : ContinuousAt (fun x â†¦ |f x|) a :=
  Filter.Tendsto.abs h

protected theorem ContinuousWithinAt.abs (h : ContinuousWithinAt f s a) :
    ContinuousWithinAt (fun x â†¦ |f x|) s a :=
  Filter.Tendsto.abs h

@[fun_prop]
protected theorem ContinuousOn.abs (h : ContinuousOn f s) : ContinuousOn (fun x â†¦ |f x|) s :=
  fun x hx => (h x hx).abs

theorem tendsto_abs_nhdsWithin_zero : Tendsto (abs : G â†’ G) (ğ“[â‰ ] 0) (ğ“[>] 0) :=
  (continuous_abs.tendsto' (0 : G) 0 abs_zero).inf <|
    tendsto_principal_principal.2 fun _x => abs_pos.2

/-- In a linearly ordered additive group, the integer multiples of an element are dense
iff they are the whole group. -/
theorem denseRange_zsmul_iff_surjective {a : G} :
    DenseRange (Â· â€¢ a : â„¤ â†’ G) â†” Surjective (Â· â€¢ a : â„¤ â†’ G) := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ h.denseRangeâŸ©
  wlog haâ‚€ : 0 < a generalizing a
  Â· simp only [â† range_eq_univ, DenseRange] at *
    rcases (not_lt.1 haâ‚€).eq_or_lt with rfl | hlt
    Â· simpa only [smul_zero, range_const, dense_iff_closure_eq, closure_singleton] using h
    Â· have H : range (Â· â€¢ -a : â„¤ â†’ G) = range (Â· â€¢ a : â„¤ â†’ G) := by
        simpa only [smul_neg, â† neg_smul] using neg_surjective.range_comp (Â· â€¢ a)
      rw [â† H]
      apply this <;> simpa only [H, neg_pos]
  intro b
  obtain âŸ¨m, hm, hm'âŸ© : âˆƒ m : â„¤, m â€¢ a âˆˆ Ioo b (b + a + a) := by
    have hne : (Ioo b (b + a + a)).Nonempty := âŸ¨b + a, by simpaâŸ©
    simpa using h.exists_mem_open isOpen_Ioo hne
  rcases eq_or_ne b ((m - 1) â€¢ a) with rfl | hne; Â· simp
  suffices (Ioo (m â€¢ a) ((m + 1) â€¢ a)).Nonempty by
    rcases h.exists_mem_open isOpen_Ioo this with âŸ¨l, hlâŸ©
    have : m < l âˆ§ l < m + 1 := by simpa [zsmul_lt_zsmul_iff_left haâ‚€] using hl
    omega
  rcases hne.lt_or_lt with hlt | hlt
  Â· refine âŸ¨b + a + a, hm', ?_âŸ©
    simpa only [add_smul, sub_smul, one_smul, lt_sub_iff_add_lt, add_lt_add_iff_right] using hlt
  Â· use b + a
    simp only [mem_Ioo, add_smul, sub_smul, one_smul, add_lt_add_iff_right] at hlt âŠ¢
    exact âŸ¨sub_lt_iff_lt_add.1 hlt, hmâŸ©

/-- In a nontrivial densely linearly ordered additive group,
the integer multiples of an element can't be dense. -/
theorem not_denseRange_zsmul [Nontrivial G] [DenselyOrdered G] {a : G} :
    Â¬DenseRange (Â· â€¢ a : â„¤ â†’ G) :=
  denseRange_zsmul_iff_surjective.not.mpr fun h â†¦
    not_isAddCyclic_of_denselyOrdered G âŸ¨âŸ¨a, hâŸ©âŸ©
