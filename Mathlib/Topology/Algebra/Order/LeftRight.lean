/-
Copyright (c) 2021 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import Mathlib.Topology.ContinuousOn

#align_import topology.algebra.order.left_right from "leanprover-community/mathlib"@"bcfa726826abd57587355b4b5b7e78ad6527b7e4"

/-!
# Left and right continuity

In this file we prove a few lemmas about left and right continuous functions:

* `continuousWithinAt_Ioi_iff_Ici`: two definitions of right continuity
  (with `(a, ‚àû)` and with `[a, ‚àû)`) are equivalent;
* `continuousWithinAt_Iio_iff_Iic`: two definitions of left continuity
  (with `(-‚àû, a)` and with `(-‚àû, a]`) are equivalent;
* `continuousAt_iff_continuous_left_right`, `continuousAt_iff_continuous_left'_right'` :
  a function is continuous at `a` if and only if it is left and right continuous at `a`.

## Tags

left continuous, right continuous
-/


open Set Filter Topology

section PartialOrder

variable {Œ± Œ≤ : Type*} [TopologicalSpace Œ±] [PartialOrder Œ±] [TopologicalSpace Œ≤]

theorem continuousWithinAt_Ioi_iff_Ici {a : Œ±} {f : Œ± ‚Üí Œ≤} :
    ContinuousWithinAt f (Ioi a) a ‚Üî ContinuousWithinAt f (Ici a) a := by
  simp only [‚Üê Ici_diff_left, continuousWithinAt_diff_self]
#align continuous_within_at_Ioi_iff_Ici continuousWithinAt_Ioi_iff_Ici

theorem continuousWithinAt_Iio_iff_Iic {a : Œ±} {f : Œ± ‚Üí Œ≤} :
    ContinuousWithinAt f (Iio a) a ‚Üî ContinuousWithinAt f (Iic a) a :=
  @continuousWithinAt_Ioi_iff_Ici Œ±·µí·µà _ _ _ _ _ f
#align continuous_within_at_Iio_iff_Iic continuousWithinAt_Iio_iff_Iic

theorem nhds_left'_le_nhds_ne (a : Œ±) : ùìù[<] a ‚â§ ùìù[‚â†] a :=
  nhdsWithin_mono a fun _ => ne_of_lt
#align nhds_left'_le_nhds_ne nhds_left'_le_nhds_ne

theorem nhds_right'_le_nhds_ne (a : Œ±) : ùìù[>] a ‚â§ ùìù[‚â†] a :=
  nhdsWithin_mono a fun _ => ne_of_gt
#align nhds_right'_le_nhds_ne nhds_right'_le_nhds_ne

end PartialOrder

section TopologicalSpace

variable {Œ± Œ≤ : Type*} [TopologicalSpace Œ±] [LinearOrder Œ±] [TopologicalSpace Œ≤]

theorem nhds_left_sup_nhds_right (a : Œ±) : ùìù[‚â§] a ‚äî ùìù[‚â•] a = ùìù a := by
  rw [‚Üê nhdsWithin_union, Iic_union_Ici, nhdsWithin_univ]
#align nhds_left_sup_nhds_right nhds_left_sup_nhds_right

theorem nhds_left'_sup_nhds_right (a : Œ±) : ùìù[<] a ‚äî ùìù[‚â•] a = ùìù a := by
  rw [‚Üê nhdsWithin_union, Iio_union_Ici, nhdsWithin_univ]
#align nhds_left'_sup_nhds_right nhds_left'_sup_nhds_right

theorem nhds_left_sup_nhds_right' (a : Œ±) : ùìù[‚â§] a ‚äî ùìù[>] a = ùìù a := by
  rw [‚Üê nhdsWithin_union, Iic_union_Ioi, nhdsWithin_univ]
#align nhds_left_sup_nhds_right' nhds_left_sup_nhds_right'

theorem nhds_left'_sup_nhds_right' (a : Œ±) : ùìù[<] a ‚äî ùìù[>] a = ùìù[‚â†] a := by
  rw [‚Üê nhdsWithin_union, Iio_union_Ioi]
#align nhds_left'_sup_nhds_right' nhds_left'_sup_nhds_right'

theorem continuousAt_iff_continuous_left_right {a : Œ±} {f : Œ± ‚Üí Œ≤} :
    ContinuousAt f a ‚Üî ContinuousWithinAt f (Iic a) a ‚àß ContinuousWithinAt f (Ici a) a := by
  simp only [ContinuousWithinAt, ContinuousAt, ‚Üê tendsto_sup, nhds_left_sup_nhds_right]
#align continuous_at_iff_continuous_left_right continuousAt_iff_continuous_left_right

theorem continuousAt_iff_continuous_left'_right' {a : Œ±} {f : Œ± ‚Üí Œ≤} :
    ContinuousAt f a ‚Üî ContinuousWithinAt f (Iio a) a ‚àß ContinuousWithinAt f (Ioi a) a := by
  rw [continuousWithinAt_Ioi_iff_Ici, continuousWithinAt_Iio_iff_Iic,
    continuousAt_iff_continuous_left_right]
#align continuous_at_iff_continuous_left'_right' continuousAt_iff_continuous_left'_right'

end TopologicalSpace
