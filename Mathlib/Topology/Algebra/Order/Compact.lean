/-
Copyright (c) 2021 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Yury Kudryashov
-/
import Mathlib.Topology.Order.LocalExtr
import Mathlib.Topology.Order.IntermediateValue
import Mathlib.Topology.Support
import Mathlib.Topology.Order.IsLUB

#align_import topology.algebra.order.compact from "leanprover-community/mathlib"@"3efd324a3a31eaa40c9d5bfc669c4fafee5f9423"

/-!
# Compactness of a closed interval

In this file we prove that a closed interval in a conditionally complete linear ordered type with
order topology (or a product of such types) is compact.

We prove the extreme value theorem (`IsCompact.exists_isMinOn`, `IsCompact.exists_isMaxOn`):
a continuous function on a compact set takes its minimum and maximum values. We provide many
variations of this theorem.

We also prove that the image of a closed interval under a continuous map is a closed interval, see
`ContinuousOn.image_Icc`.

## Tags

compact, extreme value theorem
-/

open Filter OrderDual TopologicalSpace Function Set

open scoped Filter Topology

/-!
### Compactness of a closed interval

In this section we define a typeclass `CompactIccSpace Œ±` saying that all closed intervals in `Œ±`
are compact. Then we provide an instance for a `ConditionallyCompleteLinearOrder` and prove that
the product (both `Œ± √ó Œ≤` and an indexed product) of spaces with this property inherits the
property.

We also prove some simple lemmas about spaces with this property.
-/


/-- This typeclass says that all closed intervals in `Œ±` are compact. This is true for all
conditionally complete linear orders with order topology and products (finite or infinite)
of such spaces. -/
class CompactIccSpace (Œ± : Type*) [TopologicalSpace Œ±] [Preorder Œ±] : Prop where
  /-- A closed interval `Set.Icc a b` is a compact set for all `a` and `b`. -/
  isCompact_Icc : ‚àÄ {a b : Œ±}, IsCompact (Icc a b)
#align compact_Icc_space CompactIccSpace

export CompactIccSpace (isCompact_Icc)

variable {Œ± : Type*}

-- Porting note (#10756): new lemma;
-- Porting note (#11215): TODO: make it the definition
lemma CompactIccSpace.mk' [TopologicalSpace Œ±] [Preorder Œ±]
    (h : ‚àÄ {a b : Œ±}, a ‚â§ b ‚Üí IsCompact (Icc a b)) : CompactIccSpace Œ± where
  isCompact_Icc {a b} := by_cases h fun hab => by rw [Icc_eq_empty hab]; exact isCompact_empty

-- Porting note (#10756): new lemma;
-- Porting note (#11215): TODO: drop one `'`
lemma CompactIccSpace.mk'' [TopologicalSpace Œ±] [PartialOrder Œ±]
    (h : ‚àÄ {a b : Œ±}, a < b ‚Üí IsCompact (Icc a b)) : CompactIccSpace Œ± :=
  .mk' fun hab => hab.eq_or_lt.elim (by rintro rfl; simp) h

instance [TopologicalSpace Œ±] [Preorder Œ±] [CompactIccSpace Œ±] : CompactIccSpace (Œ±·µí·µà) where
  isCompact_Icc := by
    intro a b
    convert isCompact_Icc (Œ± := Œ±) (a := b) (b := a) using 1
    exact dual_Icc (Œ± := Œ±)

/-- A closed interval in a conditionally complete linear order is compact. -/
instance (priority := 100) ConditionallyCompleteLinearOrder.toCompactIccSpace (Œ± : Type*)
    [ConditionallyCompleteLinearOrder Œ±] [TopologicalSpace Œ±] [OrderTopology Œ±] :
    CompactIccSpace Œ± := by
  refine .mk'' fun {a b} hlt => ?_
  rcases le_or_lt a b with hab | hab
  swap
  ¬∑ simp [hab]
  refine isCompact_iff_ultrafilter_le_nhds.2 fun f hf => ?_
  contrapose! hf
  rw [le_principal_iff]
  have hpt : ‚àÄ x ‚àà Icc a b, {x} ‚àâ f := fun x hx hxf =>
    hf x hx ((le_pure_iff.2 hxf).trans (pure_le_nhds x))
  set s := { x ‚àà Icc a b | Icc a x ‚àâ f }
  have hsb : b ‚àà upperBounds s := fun x hx => hx.1.2
  have sbd : BddAbove s := ‚ü®b, hsb‚ü©
  have ha : a ‚àà s := by simp [s, hpt, hab]
  rcases hab.eq_or_lt with (rfl | _hlt)
  ¬∑ exact ha.2
  -- Porting note: the `obtain` below was instead
  -- `set c := Sup s`
  -- `have hsc : IsLUB s c := isLUB_csSup ‚ü®a, ha‚ü© sbd`
  obtain ‚ü®c, hsc‚ü© : ‚àÉ c, IsLUB s c := ‚ü®sSup s, isLUB_csSup ‚ü®a, ha‚ü© ‚ü®b, hsb‚ü©‚ü©
  have hc : c ‚àà Icc a b := ‚ü®hsc.1 ha, hsc.2 hsb‚ü©
  specialize hf c hc
  have hcs : c ‚àà s := by
    rcases hc.1.eq_or_lt with (rfl | hlt); ¬∑ assumption
    refine ‚ü®hc, fun hcf => hf fun U hU => ?_‚ü©
    rcases (mem_nhdsWithin_Iic_iff_exists_Ioc_subset' hlt).1 (mem_nhdsWithin_of_mem_nhds hU)
      with ‚ü®x, hxc, hxU‚ü©
    rcases ((hsc.frequently_mem ‚ü®a, ha‚ü©).and_eventually
      (Ioc_mem_nhdsWithin_Iic ‚ü®hxc, le_rfl‚ü©)).exists with ‚ü®y, ‚ü®_hyab, hyf‚ü©, hy‚ü©
    refine mem_of_superset (f.diff_mem_iff.2 ‚ü®hcf, hyf‚ü©) (Subset.trans ?_ hxU)
    rw [diff_subset_iff]
    exact Subset.trans Icc_subset_Icc_union_Ioc <| union_subset_union Subset.rfl <|
      Ioc_subset_Ioc_left hy.1.le
  rcases hc.2.eq_or_lt with (rfl | hlt)
  ¬∑ exact hcs.2
  exfalso
  refine hf fun U hU => ?_
  rcases (mem_nhdsWithin_Ici_iff_exists_mem_Ioc_Ico_subset hlt).1
      (mem_nhdsWithin_of_mem_nhds hU) with
    ‚ü®y, hxy, hyU‚ü©
  refine mem_of_superset ?_ hyU; clear! U
  have hy : y ‚àà Icc a b := ‚ü®hc.1.trans hxy.1.le, hxy.2‚ü©
  by_cases hay : Icc a y ‚àà f
  ¬∑ refine mem_of_superset (f.diff_mem_iff.2 ‚ü®f.diff_mem_iff.2 ‚ü®hay, hcs.2‚ü©, hpt y hy‚ü©) ?_
    rw [diff_subset_iff, union_comm, Ico_union_right hxy.1.le, diff_subset_iff]
    exact Icc_subset_Icc_union_Icc
  ¬∑ exact ((hsc.1 ‚ü®hy, hay‚ü©).not_lt hxy.1).elim
#align conditionally_complete_linear_order.to_compact_Icc_space ConditionallyCompleteLinearOrder.toCompactIccSpace

instance {Œπ : Type*} {Œ± : Œπ ‚Üí Type*} [‚àÄ i, Preorder (Œ± i)] [‚àÄ i, TopologicalSpace (Œ± i)]
    [‚àÄ i, CompactIccSpace (Œ± i)] : CompactIccSpace (‚àÄ i, Œ± i) :=
  ‚ü®fun {a b} => (pi_univ_Icc a b ‚ñ∏ isCompact_univ_pi) fun _ => isCompact_Icc‚ü©

instance Pi.compact_Icc_space' {Œ± Œ≤ : Type*} [Preorder Œ≤] [TopologicalSpace Œ≤]
    [CompactIccSpace Œ≤] : CompactIccSpace (Œ± ‚Üí Œ≤) :=
  inferInstance
#align pi.compact_Icc_space' Pi.compact_Icc_space'

instance {Œ± Œ≤ : Type*} [Preorder Œ±] [TopologicalSpace Œ±] [CompactIccSpace Œ±] [Preorder Œ≤]
    [TopologicalSpace Œ≤] [CompactIccSpace Œ≤] : CompactIccSpace (Œ± √ó Œ≤) :=
  ‚ü®fun {a b} => (Icc_prod_eq a b).symm ‚ñ∏ isCompact_Icc.prod isCompact_Icc‚ü©

/-- An unordered closed interval is compact. -/
theorem isCompact_uIcc {Œ± : Type*} [LinearOrder Œ±] [TopologicalSpace Œ±] [CompactIccSpace Œ±]
    {a b : Œ±} : IsCompact (uIcc a b) :=
  isCompact_Icc
#align is_compact_uIcc isCompact_uIcc

-- See note [lower instance priority]
/-- A complete linear order is a compact space.

We do not register an instance for a `[CompactIccSpace Œ±]` because this would only add instances
for products (indexed or not) of complete linear orders, and we have instances with higher priority
that cover these cases. -/
instance (priority := 100) compactSpace_of_completeLinearOrder {Œ± : Type*} [CompleteLinearOrder Œ±]
    [TopologicalSpace Œ±] [OrderTopology Œ±] : CompactSpace Œ± :=
  ‚ü®by simp only [‚Üê Icc_bot_top, isCompact_Icc]‚ü©
#align compact_space_of_complete_linear_order compactSpace_of_completeLinearOrder

section

variable {Œ± : Type*} [Preorder Œ±] [TopologicalSpace Œ±] [CompactIccSpace Œ±]

instance compactSpace_Icc (a b : Œ±) : CompactSpace (Icc a b) :=
  isCompact_iff_compactSpace.mp isCompact_Icc
#align compact_space_Icc compactSpace_Icc

end

/-!
### Extreme value theorem
-/

section LinearOrder

variable {Œ± Œ≤ Œ≥ : Type*} [LinearOrder Œ±] [TopologicalSpace Œ±]
  [TopologicalSpace Œ≤] [TopologicalSpace Œ≥]

theorem IsCompact.exists_isLeast [ClosedIicTopology Œ±] {s : Set Œ±} (hs : IsCompact s)
    (ne_s : s.Nonempty) : ‚àÉ x, IsLeast s x := by
  haveI : Nonempty s := ne_s.to_subtype
  suffices (s ‚à© ‚ãÇ x ‚àà s, Iic x).Nonempty from
    ‚ü®this.choose, this.choose_spec.1, mem_iInter‚ÇÇ.mp this.choose_spec.2‚ü©
  rw [biInter_eq_iInter]
  by_contra H
  rw [not_nonempty_iff_eq_empty] at H
  rcases hs.elim_directed_family_closed (fun x : s => Iic ‚Üëx) (fun x => isClosed_Iic) H
      (Monotone.directed_ge fun _ _ h => Iic_subset_Iic.mpr h) with ‚ü®x, hx‚ü©
  exact not_nonempty_iff_eq_empty.mpr hx ‚ü®x, x.2, le_rfl‚ü©
#align is_compact.exists_is_least IsCompact.exists_isLeast

theorem IsCompact.exists_isGreatest [ClosedIciTopology Œ±] {s : Set Œ±} (hs : IsCompact s)
    (ne_s : s.Nonempty) : ‚àÉ x, IsGreatest s x :=
  IsCompact.exists_isLeast (Œ± := Œ±·µí·µà) hs ne_s
#align is_compact.exists_is_greatest IsCompact.exists_isGreatest

theorem IsCompact.exists_isGLB [ClosedIicTopology Œ±] {s : Set Œ±} (hs : IsCompact s)
    (ne_s : s.Nonempty) : ‚àÉ x ‚àà s, IsGLB s x :=
  (hs.exists_isLeast ne_s).imp (fun x (hx : IsLeast s x) => ‚ü®hx.1, hx.isGLB‚ü©)
#align is_compact.exists_is_glb IsCompact.exists_isGLB

theorem IsCompact.exists_isLUB [ClosedIciTopology Œ±] {s : Set Œ±} (hs : IsCompact s)
    (ne_s : s.Nonempty) : ‚àÉ x ‚àà s, IsLUB s x :=
  IsCompact.exists_isGLB (Œ± := Œ±·µí·µà) hs ne_s
#align is_compact.exists_is_lub IsCompact.exists_isLUB

theorem cocompact_le_atBot_atTop [CompactIccSpace Œ±] :
    cocompact Œ± ‚â§ atBot ‚äî atTop := by
  refine fun s hs ‚Ü¶ mem_cocompact.mpr <| (isEmpty_or_nonempty Œ±).casesOn ?_ ?_ <;> intro
  ¬∑ exact ‚ü®‚àÖ, isCompact_empty, fun x _ ‚Ü¶ (IsEmpty.false x).elim‚ü©
  ¬∑ obtain ‚ü®t, ht‚ü© := mem_atBot_sets.mp hs.1
    obtain ‚ü®u, hu‚ü© := mem_atTop_sets.mp hs.2
    refine ‚ü®Icc t u, isCompact_Icc, fun x hx ‚Ü¶ ?_‚ü©
    exact (not_and_or.mp hx).casesOn (fun h ‚Ü¶ ht x (le_of_not_le h)) fun h ‚Ü¶ hu x (le_of_not_le h)

theorem cocompact_le_atBot [OrderTop Œ±] [CompactIccSpace Œ±] :
    cocompact Œ± ‚â§ atBot := by
  refine fun _ hs ‚Ü¶ mem_cocompact.mpr <| (isEmpty_or_nonempty Œ±).casesOn ?_ ?_ <;> intro
  ¬∑ exact ‚ü®‚àÖ, isCompact_empty, fun x _ ‚Ü¶ (IsEmpty.false x).elim‚ü©
  ¬∑ obtain ‚ü®t, ht‚ü© := mem_atBot_sets.mp hs
    refine ‚ü®Icc t ‚ä§, isCompact_Icc, fun _ hx ‚Ü¶ ?_‚ü©
    exact (not_and_or.mp hx).casesOn (fun h ‚Ü¶ ht _ (le_of_not_le h)) (fun h ‚Ü¶ (h le_top).elim)

theorem cocompact_le_atTop [OrderBot Œ±] [CompactIccSpace Œ±] :
    cocompact Œ± ‚â§ atTop :=
  cocompact_le_atBot (Œ± := Œ±·µí·µà)

theorem atBot_le_cocompact [NoMinOrder Œ±] [ClosedIicTopology Œ±] :
    atBot ‚â§ cocompact Œ± := by
  refine fun s hs ‚Ü¶ ?_
  obtain ‚ü®t, ht, hts‚ü© := mem_cocompact.mp hs
  refine (Set.eq_empty_or_nonempty t).casesOn (fun h_empty ‚Ü¶ ?_) (fun h_nonempty ‚Ü¶ ?_)
  ¬∑ rewrite [compl_univ_iff.mpr h_empty, univ_subset_iff] at hts
    convert univ_mem
  ¬∑ haveI := h_nonempty.nonempty
    obtain ‚ü®a, ha‚ü© := ht.exists_isLeast h_nonempty
    obtain ‚ü®b, hb‚ü© := exists_lt a
    exact Filter.mem_atBot_sets.mpr ‚ü®b, fun b' hb' ‚Ü¶ hts <| Classical.byContradiction
      fun hc ‚Ü¶ LT.lt.false <| hb'.trans_lt <| hb.trans_le <| ha.2 (not_not_mem.mp hc)‚ü©

theorem atTop_le_cocompact [NoMaxOrder Œ±] [ClosedIciTopology Œ±] :
    atTop ‚â§ cocompact Œ± :=
  atBot_le_cocompact (Œ± := Œ±·µí·µà)

theorem atBot_atTop_le_cocompact [NoMinOrder Œ±] [NoMaxOrder Œ±]
    [OrderClosedTopology Œ±] : atBot ‚äî atTop ‚â§ cocompact Œ± :=
  sup_le atBot_le_cocompact atTop_le_cocompact

@[simp 900]
theorem cocompact_eq_atBot_atTop [NoMaxOrder Œ±] [NoMinOrder Œ±]
    [OrderClosedTopology Œ±] [CompactIccSpace Œ±] : cocompact Œ± = atBot ‚äî atTop :=
  cocompact_le_atBot_atTop.antisymm atBot_atTop_le_cocompact

@[simp]
theorem cocompact_eq_atBot [NoMinOrder Œ±] [OrderTop Œ±]
    [ClosedIicTopology Œ±] [CompactIccSpace Œ±] : cocompact Œ± = atBot :=
  cocompact_le_atBot.antisymm atBot_le_cocompact

@[simp]
theorem cocompact_eq_atTop [NoMaxOrder Œ±] [OrderBot Œ±]
    [ClosedIciTopology Œ±] [CompactIccSpace Œ±] : cocompact Œ± = atTop :=
  cocompact_le_atTop.antisymm atTop_le_cocompact

-- Porting note (#10756): new lemma; defeq to the old one but allows us to use dot notation
/-- The **extreme value theorem**: a continuous function realizes its minimum on a compact set. -/
theorem IsCompact.exists_isMinOn [ClosedIicTopology Œ±] {s : Set Œ≤} (hs : IsCompact s)
    (ne_s : s.Nonempty) {f : Œ≤ ‚Üí Œ±} (hf : ContinuousOn f s) : ‚àÉ x ‚àà s, IsMinOn f s x := by
  rcases (hs.image_of_continuousOn hf).exists_isLeast (ne_s.image f) with ‚ü®_, ‚ü®x, hxs, rfl‚ü©, hx‚ü©
  exact ‚ü®x, hxs, forall_mem_image.1 hx‚ü©

/-- If a continuous function lies strictly above `a` on a compact set,
  it has a lower bound strictly above `a`. -/
theorem IsCompact.exists_forall_le' [ClosedIicTopology Œ±] [NoMaxOrder Œ±] {f : Œ≤ ‚Üí Œ±}
    {s : Set Œ≤} (hs : IsCompact s) (hf : ContinuousOn f s) {a : Œ±} (hf' : ‚àÄ b ‚àà s, a < f b) :
    ‚àÉ a', a < a' ‚àß ‚àÄ b ‚àà s, a' ‚â§ f b := by
  rcases s.eq_empty_or_nonempty with (rfl | hs')
  ¬∑ obtain ‚ü®a', ha'‚ü© := exists_gt a
    exact ‚ü®a', ha', fun _ a ‚Ü¶ a.elim‚ü©
  ¬∑ obtain ‚ü®x, hx, hx'‚ü© := hs.exists_isMinOn hs' hf
    exact ‚ü®f x, hf' x hx, hx'‚ü©

/-- The **extreme value theorem**: a continuous function realizes its minimum on a compact set. -/
@[deprecated IsCompact.exists_isMinOn]
theorem IsCompact.exists_forall_le [ClosedIicTopology Œ±] {s : Set Œ≤} (hs : IsCompact s)
    (ne_s : s.Nonempty) {f : Œ≤ ‚Üí Œ±} (hf : ContinuousOn f s) : ‚àÉ x ‚àà s, ‚àÄ y ‚àà s, f x ‚â§ f y :=
  hs.exists_isMinOn ne_s hf
#align is_compact.exists_forall_le IsCompact.exists_forall_le

-- Porting note (#10756): new lemma; defeq to the old one but allows us to use dot notation
/-- The **extreme value theorem**: a continuous function realizes its maximum on a compact set. -/
theorem IsCompact.exists_isMaxOn [ClosedIciTopology Œ±] {s : Set Œ≤} (hs : IsCompact s)
    (ne_s : s.Nonempty) {f : Œ≤ ‚Üí Œ±} (hf : ContinuousOn f s) : ‚àÉ x ‚àà s, IsMaxOn f s x :=
  IsCompact.exists_isMinOn (Œ± := Œ±·µí·µà) hs ne_s hf

/-- The **extreme value theorem**: a continuous function realizes its maximum on a compact set. -/
@[deprecated IsCompact.exists_isMaxOn]
theorem IsCompact.exists_forall_ge [ClosedIciTopology Œ±] {s : Set Œ≤} (hs : IsCompact s)
    (ne_s : s.Nonempty) {f : Œ≤ ‚Üí Œ±} (hf : ContinuousOn f s) : ‚àÉ x ‚àà s, ‚àÄ y ‚àà s, f y ‚â§ f x :=
  IsCompact.exists_isMaxOn hs ne_s hf
#align is_compact.exists_forall_ge IsCompact.exists_forall_ge

/-- The **extreme value theorem**: if a function `f` is continuous on a closed set `s` and it is
larger than a value in its image away from compact sets, then it has a minimum on this set. -/
theorem ContinuousOn.exists_isMinOn' [ClosedIicTopology Œ±] {s : Set Œ≤} {f : Œ≤ ‚Üí Œ±}
    (hf : ContinuousOn f s) (hsc : IsClosed s) {x‚ÇÄ : Œ≤} (h‚ÇÄ : x‚ÇÄ ‚àà s)
    (hc : ‚àÄ·∂† x in cocompact Œ≤ ‚äì ùìü s, f x‚ÇÄ ‚â§ f x) : ‚àÉ x ‚àà s, IsMinOn f s x := by
  rcases (hasBasis_cocompact.inf_principal _).eventually_iff.1 hc with ‚ü®K, hK, hKf‚ü©
  have hsub : insert x‚ÇÄ (K ‚à© s) ‚äÜ s := insert_subset_iff.2 ‚ü®h‚ÇÄ, inter_subset_right‚ü©
  obtain ‚ü®x, hx, hxf‚ü© : ‚àÉ x ‚àà insert x‚ÇÄ (K ‚à© s), ‚àÄ y ‚àà insert x‚ÇÄ (K ‚à© s), f x ‚â§ f y :=
    ((hK.inter_right hsc).insert x‚ÇÄ).exists_isMinOn (insert_nonempty _ _) (hf.mono hsub)
  refine ‚ü®x, hsub hx, fun y hy => ?_‚ü©
  by_cases hyK : y ‚àà K
  exacts [hxf _ (Or.inr ‚ü®hyK, hy‚ü©), (hxf _ (Or.inl rfl)).trans (hKf ‚ü®hyK, hy‚ü©)]

/-- The **extreme value theorem**: if a function `f` is continuous on a closed set `s` and it is
larger than a value in its image away from compact sets, then it has a minimum on this set. -/
@[deprecated ContinuousOn.exists_isMinOn']
theorem ContinuousOn.exists_forall_le' [ClosedIicTopology Œ±] {s : Set Œ≤} {f : Œ≤ ‚Üí Œ±}
    (hf : ContinuousOn f s) (hsc : IsClosed s) {x‚ÇÄ : Œ≤} (h‚ÇÄ : x‚ÇÄ ‚àà s)
    (hc : ‚àÄ·∂† x in cocompact Œ≤ ‚äì ùìü s, f x‚ÇÄ ‚â§ f x) : ‚àÉ x ‚àà s, ‚àÄ y ‚àà s, f x ‚â§ f y :=
  hf.exists_isMinOn' hsc h‚ÇÄ hc
#align continuous_on.exists_forall_le' ContinuousOn.exists_forall_le'

/-- The **extreme value theorem**: if a function `f` is continuous on a closed set `s` and it is
smaller than a value in its image away from compact sets, then it has a maximum on this set. -/
theorem ContinuousOn.exists_isMaxOn' [ClosedIciTopology Œ±] {s : Set Œ≤} {f : Œ≤ ‚Üí Œ±}
    (hf : ContinuousOn f s) (hsc : IsClosed s) {x‚ÇÄ : Œ≤} (h‚ÇÄ : x‚ÇÄ ‚àà s)
    (hc : ‚àÄ·∂† x in cocompact Œ≤ ‚äì ùìü s, f x ‚â§ f x‚ÇÄ) : ‚àÉ x ‚àà s, IsMaxOn f s x :=
  ContinuousOn.exists_isMinOn' (Œ± := Œ±·µí·µà) hf hsc h‚ÇÄ hc

/-- The **extreme value theorem**: if a function `f` is continuous on a closed set `s` and it is
smaller than a value in its image away from compact sets, then it has a maximum on this set. -/
@[deprecated ContinuousOn.exists_isMaxOn']
theorem ContinuousOn.exists_forall_ge' [ClosedIciTopology Œ±] {s : Set Œ≤} {f : Œ≤ ‚Üí Œ±}
    (hf : ContinuousOn f s) (hsc : IsClosed s) {x‚ÇÄ : Œ≤} (h‚ÇÄ : x‚ÇÄ ‚àà s)
    (hc : ‚àÄ·∂† x in cocompact Œ≤ ‚äì ùìü s, f x ‚â§ f x‚ÇÄ) : ‚àÉ x ‚àà s, ‚àÄ y ‚àà s, f y ‚â§ f x :=
  hf.exists_isMaxOn' hsc h‚ÇÄ hc
#align continuous_on.exists_forall_ge' ContinuousOn.exists_forall_ge'

/-- The **extreme value theorem**: if a continuous function `f` is larger than a value in its range
away from compact sets, then it has a global minimum. -/
theorem Continuous.exists_forall_le' [ClosedIicTopology Œ±] {f : Œ≤ ‚Üí Œ±} (hf : Continuous f)
    (x‚ÇÄ : Œ≤) (h : ‚àÄ·∂† x in cocompact Œ≤, f x‚ÇÄ ‚â§ f x) : ‚àÉ x : Œ≤, ‚àÄ y : Œ≤, f x ‚â§ f y :=
  let ‚ü®x, _, hx‚ü© := hf.continuousOn.exists_isMinOn' isClosed_univ (mem_univ x‚ÇÄ)
    (by rwa [principal_univ, inf_top_eq])
  ‚ü®x, fun y => hx (mem_univ y)‚ü©
#align continuous.exists_forall_le' Continuous.exists_forall_le'

/-- The **extreme value theorem**: if a continuous function `f` is smaller than a value in its range
away from compact sets, then it has a global maximum. -/
theorem Continuous.exists_forall_ge' [ClosedIciTopology Œ±] {f : Œ≤ ‚Üí Œ±} (hf : Continuous f)
    (x‚ÇÄ : Œ≤) (h : ‚àÄ·∂† x in cocompact Œ≤, f x ‚â§ f x‚ÇÄ) : ‚àÉ x : Œ≤, ‚àÄ y : Œ≤, f y ‚â§ f x :=
  Continuous.exists_forall_le' (Œ± := Œ±·µí·µà) hf x‚ÇÄ h
#align continuous.exists_forall_ge' Continuous.exists_forall_ge'

/-- The **extreme value theorem**: if a continuous function `f` tends to infinity away from compact
sets, then it has a global minimum. -/
theorem Continuous.exists_forall_le [ClosedIicTopology Œ±] [Nonempty Œ≤] {f : Œ≤ ‚Üí Œ±}
    (hf : Continuous f) (hlim : Tendsto f (cocompact Œ≤) atTop) : ‚àÉ x, ‚àÄ y, f x ‚â§ f y := by
  inhabit Œ≤
  exact hf.exists_forall_le' default (hlim.eventually <| eventually_ge_atTop _)
#align continuous.exists_forall_le Continuous.exists_forall_le

/-- The **extreme value theorem**: if a continuous function `f` tends to negative infinity away from
compact sets, then it has a global maximum. -/
theorem Continuous.exists_forall_ge [ClosedIciTopology Œ±] [Nonempty Œ≤] {f : Œ≤ ‚Üí Œ±}
    (hf : Continuous f) (hlim : Tendsto f (cocompact Œ≤) atBot) : ‚àÉ x, ‚àÄ y, f y ‚â§ f x :=
  Continuous.exists_forall_le (Œ± := Œ±·µí·µà) hf hlim
#align continuous.exists_forall_ge Continuous.exists_forall_ge

/-- A continuous function with compact support has a global minimum. -/
@[to_additive "A continuous function with compact support has a global minimum."]
theorem Continuous.exists_forall_le_of_hasCompactMulSupport [ClosedIicTopology Œ±] [Nonempty Œ≤]
    [One Œ±] {f : Œ≤ ‚Üí Œ±} (hf : Continuous f) (h : HasCompactMulSupport f) :
    ‚àÉ x : Œ≤, ‚àÄ y : Œ≤, f x ‚â§ f y := by
  obtain ‚ü®_, ‚ü®x, rfl‚ü©, hx‚ü© := (h.isCompact_range hf).exists_isLeast (range_nonempty _)
  rw [mem_lowerBounds, forall_mem_range] at hx
  exact ‚ü®x, hx‚ü©
#align continuous.exists_forall_le_of_has_compact_mul_support Continuous.exists_forall_le_of_hasCompactMulSupport
#align continuous.exists_forall_le_of_has_compact_support Continuous.exists_forall_le_of_hasCompactSupport

/-- A continuous function with compact support has a global maximum. -/
@[to_additive "A continuous function with compact support has a global maximum."]
theorem Continuous.exists_forall_ge_of_hasCompactMulSupport [ClosedIciTopology Œ±] [Nonempty Œ≤]
    [One Œ±] {f : Œ≤ ‚Üí Œ±} (hf : Continuous f) (h : HasCompactMulSupport f) :
    ‚àÉ x : Œ≤, ‚àÄ y : Œ≤, f y ‚â§ f x :=
  Continuous.exists_forall_le_of_hasCompactMulSupport (Œ± := Œ±·µí·µà) hf h
#align continuous.exists_forall_ge_of_has_compact_mul_support Continuous.exists_forall_ge_of_hasCompactMulSupport
#align continuous.exists_forall_ge_of_has_compact_support Continuous.exists_forall_ge_of_hasCompactSupport

/-- A compact set is bounded below -/
theorem IsCompact.bddBelow [ClosedIicTopology Œ±] [Nonempty Œ±] {s : Set Œ±} (hs : IsCompact s) :
    BddBelow s := by
  rcases s.eq_empty_or_nonempty with rfl | hne
  ¬∑ exact bddBelow_empty
  ¬∑ obtain ‚ü®a, -, has‚ü© := hs.exists_isLeast hne
    exact ‚ü®a, has‚ü©
#align is_compact.bdd_below IsCompact.bddBelow

/-- A compact set is bounded above -/
theorem IsCompact.bddAbove [ClosedIciTopology Œ±] [Nonempty Œ±] {s : Set Œ±} (hs : IsCompact s) :
    BddAbove s :=
  IsCompact.bddBelow (Œ± := Œ±·µí·µà) hs
#align is_compact.bdd_above IsCompact.bddAbove

/-- A continuous function is bounded below on a compact set. -/
theorem IsCompact.bddBelow_image [ClosedIicTopology Œ±] [Nonempty Œ±] {f : Œ≤ ‚Üí Œ±} {K : Set Œ≤}
    (hK : IsCompact K) (hf : ContinuousOn f K) : BddBelow (f '' K) :=
  (hK.image_of_continuousOn hf).bddBelow
#align is_compact.bdd_below_image IsCompact.bddBelow_image

/-- A continuous function is bounded above on a compact set. -/
theorem IsCompact.bddAbove_image [ClosedIciTopology Œ±] [Nonempty Œ±] {f : Œ≤ ‚Üí Œ±} {K : Set Œ≤}
    (hK : IsCompact K) (hf : ContinuousOn f K) : BddAbove (f '' K) :=
  IsCompact.bddBelow_image (Œ± := Œ±·µí·µà) hK hf
#align is_compact.bdd_above_image IsCompact.bddAbove_image

/-- A continuous function with compact support is bounded below. -/
@[to_additive " A continuous function with compact support is bounded below. "]
theorem Continuous.bddBelow_range_of_hasCompactMulSupport [ClosedIicTopology Œ±] [One Œ±]
    {f : Œ≤ ‚Üí Œ±} (hf : Continuous f) (h : HasCompactMulSupport f) : BddBelow (range f) :=
  (h.isCompact_range hf).bddBelow
#align continuous.bdd_below_range_of_has_compact_mul_support Continuous.bddBelow_range_of_hasCompactMulSupport
#align continuous.bdd_below_range_of_has_compact_support Continuous.bddBelow_range_of_hasCompactSupport

/-- A continuous function with compact support is bounded above. -/
@[to_additive " A continuous function with compact support is bounded above. "]
theorem Continuous.bddAbove_range_of_hasCompactMulSupport [ClosedIciTopology Œ±] [One Œ±]
    {f : Œ≤ ‚Üí Œ±} (hf : Continuous f) (h : HasCompactMulSupport f) : BddAbove (range f) :=
  Continuous.bddBelow_range_of_hasCompactMulSupport (Œ± := Œ±·µí·µà) hf h
#align continuous.bdd_above_range_of_has_compact_mul_support Continuous.bddAbove_range_of_hasCompactMulSupport
#align continuous.bdd_above_range_of_has_compact_support Continuous.bddAbove_range_of_hasCompactSupport

end LinearOrder

section ConditionallyCompleteLinearOrder

variable {Œ± Œ≤ Œ≥ : Type*} [ConditionallyCompleteLinearOrder Œ±] [TopologicalSpace Œ±]
  [TopologicalSpace Œ≤] [TopologicalSpace Œ≥]

theorem IsCompact.sSup_lt_iff_of_continuous [ClosedIciTopology Œ±] {f : Œ≤ ‚Üí Œ±} {K : Set Œ≤}
    (hK : IsCompact K) (h0K : K.Nonempty) (hf : ContinuousOn f K) (y : Œ±) :
    sSup (f '' K) < y ‚Üî ‚àÄ x ‚àà K, f x < y := by
  refine ‚ü®fun h x hx => (le_csSup (hK.bddAbove_image hf) <| mem_image_of_mem f hx).trans_lt h,
    fun h => ?_‚ü©
  obtain ‚ü®x, hx, h2x‚ü© := hK.exists_isMaxOn h0K hf
  refine (csSup_le (h0K.image f) ?_).trans_lt (h x hx)
  rintro _ ‚ü®x', hx', rfl‚ü©; exact h2x hx'
#align is_compact.Sup_lt_iff_of_continuous IsCompact.sSup_lt_iff_of_continuous

theorem IsCompact.lt_sInf_iff_of_continuous [ClosedIicTopology Œ±] {f : Œ≤ ‚Üí Œ±} {K : Set Œ≤}
    (hK : IsCompact K) (h0K : K.Nonempty) (hf : ContinuousOn f K) (y : Œ±) :
    y < sInf (f '' K) ‚Üî ‚àÄ x ‚àà K, y < f x :=
  IsCompact.sSup_lt_iff_of_continuous (Œ± := Œ±·µí·µà) hK h0K hf y
#align is_compact.lt_Inf_iff_of_continuous IsCompact.lt_sInf_iff_of_continuous

end ConditionallyCompleteLinearOrder

/-!
### Min and max elements of a compact set
-/

section InfSup

variable {Œ± Œ≤ : Type*} [ConditionallyCompleteLinearOrder Œ±] [TopologicalSpace Œ±]
  [TopologicalSpace Œ≤]

theorem IsCompact.sInf_mem [ClosedIicTopology Œ±] {s : Set Œ±} (hs : IsCompact s)
    (ne_s : s.Nonempty) : sInf s ‚àà s :=
  let ‚ü®_a, ha‚ü© := hs.exists_isLeast ne_s
  ha.csInf_mem
#align is_compact.Inf_mem IsCompact.sInf_mem

theorem IsCompact.sSup_mem [ClosedIciTopology Œ±] {s : Set Œ±} (hs : IsCompact s)
    (ne_s : s.Nonempty) : sSup s ‚àà s :=
  IsCompact.sInf_mem (Œ± := Œ±·µí·µà) hs ne_s
#align is_compact.Sup_mem IsCompact.sSup_mem

theorem IsCompact.isGLB_sInf [ClosedIicTopology Œ±] {s : Set Œ±} (hs : IsCompact s)
    (ne_s : s.Nonempty) : IsGLB s (sInf s) :=
  isGLB_csInf ne_s hs.bddBelow
#align is_compact.is_glb_Inf IsCompact.isGLB_sInf

theorem IsCompact.isLUB_sSup [ClosedIciTopology Œ±] {s : Set Œ±} (hs : IsCompact s)
    (ne_s : s.Nonempty) : IsLUB s (sSup s) :=
  IsCompact.isGLB_sInf (Œ± := Œ±·µí·µà) hs ne_s
#align is_compact.is_lub_Sup IsCompact.isLUB_sSup

theorem IsCompact.isLeast_sInf [ClosedIicTopology Œ±] {s : Set Œ±} (hs : IsCompact s)
    (ne_s : s.Nonempty) : IsLeast s (sInf s) :=
  ‚ü®hs.sInf_mem ne_s, (hs.isGLB_sInf ne_s).1‚ü©
#align is_compact.is_least_Inf IsCompact.isLeast_sInf

theorem IsCompact.isGreatest_sSup [ClosedIciTopology Œ±] {s : Set Œ±} (hs : IsCompact s)
    (ne_s : s.Nonempty) : IsGreatest s (sSup s) :=
  IsCompact.isLeast_sInf (Œ± := Œ±·µí·µà) hs ne_s
#align is_compact.is_greatest_Sup IsCompact.isGreatest_sSup

theorem IsCompact.exists_sInf_image_eq_and_le [ClosedIicTopology Œ±] {s : Set Œ≤}
    (hs : IsCompact s) (ne_s : s.Nonempty) {f : Œ≤ ‚Üí Œ±} (hf : ContinuousOn f s) :
    ‚àÉ x ‚àà s, sInf (f '' s) = f x ‚àß ‚àÄ y ‚àà s, f x ‚â§ f y :=
  let ‚ü®x, hxs, hx‚ü© := (hs.image_of_continuousOn hf).sInf_mem (ne_s.image f)
  ‚ü®x, hxs, hx.symm, fun _y hy =>
    hx.trans_le <| csInf_le (hs.image_of_continuousOn hf).bddBelow <| mem_image_of_mem f hy‚ü©
#align is_compact.exists_Inf_image_eq_and_le IsCompact.exists_sInf_image_eq_and_le

theorem IsCompact.exists_sSup_image_eq_and_ge [ClosedIciTopology Œ±] {s : Set Œ≤}
    (hs : IsCompact s) (ne_s : s.Nonempty) {f : Œ≤ ‚Üí Œ±} (hf : ContinuousOn f s) :
    ‚àÉ x ‚àà s, sSup (f '' s) = f x ‚àß ‚àÄ y ‚àà s, f y ‚â§ f x :=
  IsCompact.exists_sInf_image_eq_and_le (Œ± := Œ±·µí·µà) hs ne_s hf
#align is_compact.exists_Sup_image_eq_and_ge IsCompact.exists_sSup_image_eq_and_ge

theorem IsCompact.exists_sInf_image_eq [ClosedIicTopology Œ±] {s : Set Œ≤} (hs : IsCompact s)
    (ne_s : s.Nonempty) {f : Œ≤ ‚Üí Œ±} (hf : ContinuousOn f s) : ‚àÉ x ‚àà s, sInf (f '' s) = f x :=
  let ‚ü®x, hxs, hx, _‚ü© := hs.exists_sInf_image_eq_and_le ne_s hf
  ‚ü®x, hxs, hx‚ü©
#align is_compact.exists_Inf_image_eq IsCompact.exists_sInf_image_eq

theorem IsCompact.exists_sSup_image_eq [ClosedIciTopology Œ±] {s : Set Œ≤} (hs : IsCompact s)
    (ne_s : s.Nonempty) : ‚àÄ {f : Œ≤ ‚Üí Œ±}, ContinuousOn f s ‚Üí ‚àÉ x ‚àà s, sSup (f '' s) = f x :=
  IsCompact.exists_sInf_image_eq (Œ± := Œ±·µí·µà) hs ne_s
#align is_compact.exists_Sup_image_eq IsCompact.exists_sSup_image_eq

end InfSup

section ExistsExtr

variable {Œ± Œ≤ : Type*} [LinearOrder Œ±] [TopologicalSpace Œ±] [TopologicalSpace Œ≤]

theorem IsCompact.exists_isMinOn_mem_subset [ClosedIicTopology Œ±] {f : Œ≤ ‚Üí Œ±} {s t : Set Œ≤}
    {z : Œ≤} (ht : IsCompact t) (hf : ContinuousOn f t) (hz : z ‚àà t)
    (hfz : ‚àÄ z' ‚àà t \ s, f z < f z') : ‚àÉ x ‚àà s, IsMinOn f t x :=
  let ‚ü®x, hxt, hfx‚ü© := ht.exists_isMinOn ‚ü®z, hz‚ü© hf
  ‚ü®x, by_contra fun hxs => (hfz x ‚ü®hxt, hxs‚ü©).not_le (hfx hz), hfx‚ü©

theorem IsCompact.exists_isMaxOn_mem_subset [ClosedIciTopology Œ±] {f : Œ≤ ‚Üí Œ±} {s t : Set Œ≤}
    {z : Œ≤} (ht : IsCompact t) (hf : ContinuousOn f t) (hz : z ‚àà t)
    (hfz : ‚àÄ z' ‚àà t \ s, f z' < f z) : ‚àÉ x ‚àà s, IsMaxOn f t x :=
  let ‚ü®x, hxt, hfx‚ü© := ht.exists_isMaxOn ‚ü®z, hz‚ü© hf
  ‚ü®x, by_contra fun hxs => (hfz x ‚ü®hxt, hxs‚ü©).not_le (hfx hz), hfx‚ü©

@[deprecated IsCompact.exists_isMinOn_mem_subset]
theorem IsCompact.exists_isLocalMinOn_mem_subset [ClosedIicTopology Œ±] {f : Œ≤ ‚Üí Œ±} {s t : Set Œ≤}
    {z : Œ≤} (ht : IsCompact t) (hf : ContinuousOn f t) (hz : z ‚àà t)
    (hfz : ‚àÄ z' ‚àà t \ s, f z < f z') : ‚àÉ x ‚àà s, IsLocalMinOn f t x :=
  let ‚ü®x, hxs, h‚ü© := ht.exists_isMinOn_mem_subset hf hz hfz
  ‚ü®x, hxs, h.localize‚ü©
#align is_compact.exists_local_min_on_mem_subset IsCompact.exists_isLocalMinOn_mem_subset

-- Porting note: rfc: assume `t ‚àà ùìùÀ¢ s` (a.k.a. `s ‚äÜ interior t`) instead of `s ‚äÜ t` and
-- `IsOpen s`?
theorem IsCompact.exists_isLocalMin_mem_open [ClosedIicTopology Œ±] {f : Œ≤ ‚Üí Œ±} {s t : Set Œ≤}
    {z : Œ≤} (ht : IsCompact t) (hst : s ‚äÜ t) (hf : ContinuousOn f t) (hz : z ‚àà t)
    (hfz : ‚àÄ z' ‚àà t \ s, f z < f z') (hs : IsOpen s) : ‚àÉ x ‚àà s, IsLocalMin f x :=
  let ‚ü®x, hxs, h‚ü© := ht.exists_isMinOn_mem_subset hf hz hfz
  ‚ü®x, hxs, h.isLocalMin <| mem_nhds_iff.2 ‚ü®s, hst, hs, hxs‚ü©‚ü©
#align is_compact.exists_local_min_mem_open IsCompact.exists_isLocalMin_mem_open

theorem IsCompact.exists_isLocalMax_mem_open [ClosedIciTopology Œ±] {f : Œ≤ ‚Üí Œ±} {s t : Set Œ≤}
    {z : Œ≤} (ht : IsCompact t) (hst : s ‚äÜ t) (hf : ContinuousOn f t) (hz : z ‚àà t)
    (hfz : ‚àÄ z' ‚àà t \ s, f z' < f z) (hs : IsOpen s) : ‚àÉ x ‚àà s, IsLocalMax f x :=
  let ‚ü®x, hxs, h‚ü© := ht.exists_isMaxOn_mem_subset hf hz hfz
  ‚ü®x, hxs, h.isLocalMax <| mem_nhds_iff.2 ‚ü®s, hst, hs, hxs‚ü©‚ü©

end ExistsExtr

variable {Œ± Œ≤ Œ≥ : Type*} [ConditionallyCompleteLinearOrder Œ±] [TopologicalSpace Œ±]
  [OrderTopology Œ±] [TopologicalSpace Œ≤] [TopologicalSpace Œ≥]

theorem eq_Icc_of_connected_compact {s : Set Œ±} (h‚ÇÅ : IsConnected s) (h‚ÇÇ : IsCompact s) :
    s = Icc (sInf s) (sSup s) :=
  eq_Icc_csInf_csSup_of_connected_bdd_closed h‚ÇÅ h‚ÇÇ.bddBelow h‚ÇÇ.bddAbove h‚ÇÇ.isClosed
#align eq_Icc_of_connected_compact eq_Icc_of_connected_compact

/-- If `f : Œ≥ ‚Üí Œ≤ ‚Üí Œ±` is a function that is continuous as a function on `Œ≥ √ó Œ≤`, `Œ±` is a
conditionally complete linear order, and `K : Set Œ≤` is a compact set, then
`fun x ‚Ü¶ sSup (f x '' K)` is a continuous function. -/
/- Porting note (#11215): TODO: generalize. The following version seems to be true:
```
theorem IsCompact.tendsto_sSup {f : Œ≥ ‚Üí Œ≤ ‚Üí Œ±} {g : Œ≤ ‚Üí Œ±} {K : Set Œ≤} {l : Filter Œ≥}
    (hK : IsCompact K) (hf : ‚àÄ y ‚àà K, Tendsto ‚Üøf (l √óÀ¢ ùìù[K] y) (ùìù (g y)))
    (hgc : ContinuousOn g K) :
    Tendsto (fun x => sSup (f x '' K)) l (ùìù (sSup (g '' K))) := _
```
Moreover, it seems that `hgc` follows from `hf` (Yury Kudryashov). -/
theorem IsCompact.continuous_sSup {f : Œ≥ ‚Üí Œ≤ ‚Üí Œ±} {K : Set Œ≤} (hK : IsCompact K)
    (hf : Continuous ‚Üøf) : Continuous fun x => sSup (f x '' K) := by
  rcases eq_empty_or_nonempty K with (rfl | h0K)
  ¬∑ simp_rw [image_empty]
    exact continuous_const
  rw [continuous_iff_continuousAt]
  intro x
  obtain ‚ü®y, hyK, h2y, hy‚ü© :=
    hK.exists_sSup_image_eq_and_ge h0K
      (show Continuous fun y => f x y from hf.comp <| Continuous.Prod.mk x).continuousOn
  rw [ContinuousAt, h2y, tendsto_order]
  have := tendsto_order.mp ((show Continuous fun x => f x y
    from hf.comp <| continuous_id.prod_mk continuous_const).tendsto x)
  refine ‚ü®fun z hz => ?_, fun z hz => ?_‚ü©
  ¬∑ refine (this.1 z hz).mono fun x' hx' =>
      hx'.trans_le <| le_csSup ?_ <| mem_image_of_mem (f x') hyK
    exact hK.bddAbove_image (hf.comp <| Continuous.Prod.mk x').continuousOn
  ¬∑ have h : ({x} : Set Œ≥) √óÀ¢ K ‚äÜ ‚Üøf ‚Åª¬π' Iio z := by
      rintro ‚ü®x', y'‚ü© ‚ü®(rfl : x' = x), hy'‚ü©
      exact (hy y' hy').trans_lt hz
    obtain ‚ü®u, v, hu, _, hxu, hKv, huv‚ü© :=
      generalized_tube_lemma isCompact_singleton hK (isOpen_Iio.preimage hf) h
    refine eventually_of_mem (hu.mem_nhds (singleton_subset_iff.mp hxu)) fun x' hx' => ?_
    rw [hK.sSup_lt_iff_of_continuous h0K
        (show Continuous (f x') from hf.comp <| Continuous.Prod.mk x').continuousOn]
    exact fun y' hy' => huv (mk_mem_prod hx' (hKv hy'))
#align is_compact.continuous_Sup IsCompact.continuous_sSup

theorem IsCompact.continuous_sInf {f : Œ≥ ‚Üí Œ≤ ‚Üí Œ±} {K : Set Œ≤} (hK : IsCompact K)
    (hf : Continuous ‚Üøf) : Continuous fun x => sInf (f x '' K) :=
  IsCompact.continuous_sSup (Œ± := Œ±·µí·µà) hK hf
#align is_compact.continuous_Inf IsCompact.continuous_sInf

namespace ContinuousOn

/-!
### Image of a closed interval
-/

variable [DenselyOrdered Œ±] [ConditionallyCompleteLinearOrder Œ≤] [OrderTopology Œ≤] {f : Œ± ‚Üí Œ≤}
  {a b c : Œ±}

open scoped Interval

theorem image_Icc (hab : a ‚â§ b) (h : ContinuousOn f <| Icc a b) :
    f '' Icc a b = Icc (sInf <| f '' Icc a b) (sSup <| f '' Icc a b) :=
  eq_Icc_of_connected_compact ‚ü®(nonempty_Icc.2 hab).image f, isPreconnected_Icc.image f h‚ü©
    (isCompact_Icc.image_of_continuousOn h)
#align continuous_on.image_Icc ContinuousOn.image_Icc

theorem image_uIcc_eq_Icc (h : ContinuousOn f [[a, b]]) :
    f '' [[a, b]] = Icc (sInf (f '' [[a, b]])) (sSup (f '' [[a, b]])) :=
  image_Icc min_le_max h
#align continuous_on.image_uIcc_eq_Icc ContinuousOn.image_uIcc_eq_Icc

theorem image_uIcc (h : ContinuousOn f <| [[a, b]]) :
    f '' [[a, b]] = [[sInf (f '' [[a, b]]), sSup (f '' [[a, b]])]] := by
  refine h.image_uIcc_eq_Icc.trans (uIcc_of_le ?_).symm
  refine csInf_le_csSup ?_ ?_ (nonempty_uIcc.image _) <;> rw [h.image_uIcc_eq_Icc]
  exacts [bddBelow_Icc, bddAbove_Icc]
#align continuous_on.image_uIcc ContinuousOn.image_uIcc

theorem sInf_image_Icc_le (h : ContinuousOn f <| Icc a b) (hc : c ‚àà Icc a b) :
    sInf (f '' Icc a b) ‚â§ f c := by
  have := mem_image_of_mem f hc
  rw [h.image_Icc (hc.1.trans hc.2)] at this
  exact this.1
#align continuous_on.Inf_image_Icc_le ContinuousOn.sInf_image_Icc_le

theorem le_sSup_image_Icc (h : ContinuousOn f <| Icc a b) (hc : c ‚àà Icc a b) :
    f c ‚â§ sSup (f '' Icc a b) := by
  have := mem_image_of_mem f hc
  rw [h.image_Icc (hc.1.trans hc.2)] at this
  exact this.2
#align continuous_on.le_Sup_image_Icc ContinuousOn.le_sSup_image_Icc

end ContinuousOn
