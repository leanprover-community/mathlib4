/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Topology.Order.Basic
import Mathlib.Data.Set.Intervals.OrdConnectedComponent

#align_import topology.algebra.order.t5 from "leanprover-community/mathlib"@"4c19a16e4b705bf135cf9a80ac18fcc99c438514"

/-!
# Linear order is a completely normal Hausdorff topological space

In this file we prove that a linear order with order topology is a completely normal Hausdorff
topological space.
-/


open Filter Set Function OrderDual Topology Interval

variable {X : Type*} [LinearOrder X] [TopologicalSpace X] [OrderTopology X] {a b c : X}
  {s t : Set X}

namespace Set

@[simp]
theorem ordConnectedComponent_mem_nhds : ordConnectedComponent s a âˆˆ ğ“ a â†” s âˆˆ ğ“ a := by
  refine' âŸ¨fun h => mem_of_superset h ordConnectedComponent_subset, fun h => _âŸ©
  -- âŠ¢ ordConnectedComponent s a âˆˆ ğ“ a
  rcases exists_Icc_mem_subset_of_mem_nhds h with âŸ¨b, c, ha, ha', hsâŸ©
  -- âŠ¢ ordConnectedComponent s a âˆˆ ğ“ a
  exact mem_of_superset ha' (subset_ordConnectedComponent ha hs)
  -- ğŸ‰ no goals
#align set.ord_connected_component_mem_nhds Set.ordConnectedComponent_mem_nhds

theorem compl_section_ordSeparatingSet_mem_nhdsWithin_Ici (hd : Disjoint s (closure t))
    (ha : a âˆˆ s) : (ordConnectedSection (ordSeparatingSet s t))á¶œ âˆˆ ğ“[â‰¥] a := by
  have hmem : tá¶œ âˆˆ ğ“[â‰¥] a := by
    refine' mem_nhdsWithin_of_mem_nhds _
    rw [â† mem_interior_iff_mem_nhds, interior_compl]
    exact disjoint_left.1 hd ha
  rcases exists_Icc_mem_subset_of_mem_nhdsWithin_Ici hmem with âŸ¨b, hab, hmem', hsubâŸ©
  -- âŠ¢ (ordConnectedSection (ordSeparatingSet s t))á¶œ âˆˆ ğ“[Ici a] a
  by_cases H : Disjoint (Icc a b) (ordConnectedSection <| ordSeparatingSet s t)
  -- âŠ¢ (ordConnectedSection (ordSeparatingSet s t))á¶œ âˆˆ ğ“[Ici a] a
  Â· exact mem_of_superset hmem' (disjoint_left.1 H)
    -- ğŸ‰ no goals
  Â· simp only [Set.disjoint_left, not_forall, Classical.not_not] at H
    -- âŠ¢ (ordConnectedSection (ordSeparatingSet s t))á¶œ âˆˆ ğ“[Ici a] a
    rcases H with âŸ¨c, âŸ¨hac, hcbâŸ©, hcâŸ©
    -- âŠ¢ (ordConnectedSection (ordSeparatingSet s t))á¶œ âˆˆ ğ“[Ici a] a
    have hsub' : Icc a b âŠ† ordConnectedComponent tá¶œ a :=
      subset_ordConnectedComponent (left_mem_Icc.2 hab) hsub
    have hd : Disjoint s (ordConnectedSection (ordSeparatingSet s t)) :=
      disjoint_left_ordSeparatingSet.mono_right ordConnectedSection_subset
    replace hac : a < c := hac.lt_of_ne <| Ne.symm <| ne_of_mem_of_not_mem hc <|
      disjoint_left.1 hd ha
    refine' mem_of_superset (Ico_mem_nhdsWithin_Ici (left_mem_Ico.2 hac)) fun x hx hx' => _
    -- âŠ¢ False
    refine' hx.2.ne (eq_of_mem_ordConnectedSection_of_uIcc_subset hx' hc _)
    -- âŠ¢ [[x, c]] âŠ† ordSeparatingSet s t
    refine' subset_inter (subset_iUnionâ‚‚_of_subset a ha _) _
    -- âŠ¢ [[x, c]] âŠ† ordConnectedComponent tá¶œ a
    Â· exact OrdConnected.uIcc_subset inferInstance (hsub' âŸ¨hx.1, hx.2.le.trans hcbâŸ©)
        (hsub' âŸ¨hac.le, hcbâŸ©)
    Â· rcases mem_iUnionâ‚‚.1 (ordConnectedSection_subset hx').2 with âŸ¨y, hyt, hxyâŸ©
      -- âŠ¢ [[x, c]] âŠ† â‹ƒ (x : X) (_ : x âˆˆ t), ordConnectedComponent sá¶œ x
      refine' subset_iUnionâ‚‚_of_subset y hyt (OrdConnected.uIcc_subset inferInstance hxy _)
      -- âŠ¢ c âˆˆ ordConnectedComponent sá¶œ y
      refine' subset_ordConnectedComponent left_mem_uIcc hxy _
      -- âŠ¢ c âˆˆ [[y, x]]
      suffices c < y by
        rw [uIcc_of_ge (hx.2.trans this).le]
        exact âŸ¨hx.2.le, this.leâŸ©
      refine' lt_of_not_le fun hyc => _
      -- âŠ¢ False
      have hya : y < a := not_le.1 fun hay => hsub âŸ¨hay, hyc.trans hcbâŸ© hyt
      -- âŠ¢ False
      exact hxy (Icc_subset_uIcc âŸ¨hya.le, hx.1âŸ©) ha
      -- ğŸ‰ no goals
#align set.compl_section_ord_separating_set_mem_nhds_within_Ici Set.compl_section_ordSeparatingSet_mem_nhdsWithin_Ici

theorem compl_section_ordSeparatingSet_mem_nhdsWithin_Iic (hd : Disjoint s (closure t))
    (ha : a âˆˆ s) : (ordConnectedSection <| ordSeparatingSet s t)á¶œ âˆˆ ğ“[â‰¤] a := by
  have hd' : Disjoint (ofDual â»Â¹' s) (closure <| ofDual â»Â¹' t) := hd
  -- âŠ¢ (ordConnectedSection (ordSeparatingSet s t))á¶œ âˆˆ ğ“[Iic a] a
  have ha' : toDual a âˆˆ ofDual â»Â¹' s := ha
  -- âŠ¢ (ordConnectedSection (ordSeparatingSet s t))á¶œ âˆˆ ğ“[Iic a] a
  simpa only [dual_ordSeparatingSet, dual_ordConnectedSection] using
    compl_section_ordSeparatingSet_mem_nhdsWithin_Ici hd' ha'
#align set.compl_section_ord_separating_set_mem_nhds_within_Iic Set.compl_section_ordSeparatingSet_mem_nhdsWithin_Iic

theorem compl_section_ordSeparatingSet_mem_nhds (hd : Disjoint s (closure t)) (ha : a âˆˆ s) :
    (ordConnectedSection <| ordSeparatingSet s t)á¶œ âˆˆ ğ“ a := by
  rw [â† nhds_left_sup_nhds_right, mem_sup]
  -- âŠ¢ (ordConnectedSection (ordSeparatingSet s t))á¶œ âˆˆ ğ“[Iic a] a âˆ§ (ordConnectedSe â€¦
  exact
    âŸ¨compl_section_ordSeparatingSet_mem_nhdsWithin_Iic hd ha,
      compl_section_ordSeparatingSet_mem_nhdsWithin_Ici hd haâŸ©
#align set.compl_section_ord_separating_set_mem_nhds Set.compl_section_ordSeparatingSet_mem_nhds

theorem ordT5Nhd_mem_nhdsSet (hd : Disjoint s (closure t)) : ordT5Nhd s t âˆˆ ğ“Ë¢ s :=
  bUnion_mem_nhdsSet fun x hx => ordConnectedComponent_mem_nhds.2 <| inter_mem
    (by
      rw [â† mem_interior_iff_mem_nhds, interior_compl]
      -- âŠ¢ x âˆˆ (closure t)á¶œ
      exact disjoint_left.1 hd hx)
      -- ğŸ‰ no goals
    (compl_section_ordSeparatingSet_mem_nhds hd hx)
#align set.ord_t5_nhd_mem_nhds_set Set.ordT5Nhd_mem_nhdsSet

end Set

open Set

/-- A linear order with order topology is a completely normal Hausdorff topological space. -/
instance (priority := 100) OrderTopology.t5Space : T5Space X :=
  âŸ¨fun s t hâ‚ hâ‚‚ => Filter.disjoint_iff.2
    âŸ¨ordT5Nhd s t, ordT5Nhd_mem_nhdsSet hâ‚‚, ordT5Nhd t s, ordT5Nhd_mem_nhdsSet hâ‚.symm,
      disjoint_ordT5NhdâŸ©âŸ©
#align order_topology.t5_space OrderTopology.t5Space
