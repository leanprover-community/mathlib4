/-
Copyright (c) 2022 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import Mathlib.Topology.Order.Basic
import Mathlib.GroupTheory.Archimedean

#align_import topology.algebra.order.archimedean from "leanprover-community/mathlib"@"4c19a16e4b705bf135cf9a80ac18fcc99c438514"

/-!
# Topology on archimedean groups and fields

In this file we prove the following theorems:

- `Rat.denseRange_cast`: the coercion from `â„š` to a linear ordered archimedean field has dense
  range;

- `AddSubgroup.dense_of_not_isolated_zero`, `AddSubgroup.dense_of_no_min`: two sufficient conditions
  for a subgroup of an archimedean linear ordered additive commutative group to be dense;

- `AddSubgroup.dense_or_cyclic`: an additive subgroup of an archimedean linear ordered additive
  commutative group `G` with order topology either is dense in `G` or is a cyclic subgroup.
-/

open Set

/-- Rational numbers are dense in a linear ordered archimedean field. -/
theorem Rat.denseRange_cast {ğ•œ} [LinearOrderedField ğ•œ] [TopologicalSpace ğ•œ] [OrderTopology ğ•œ]
  [Archimedean ğ•œ] : DenseRange ((â†‘) : â„š â†’ ğ•œ) :=
  dense_of_exists_between fun _ _ h => Set.exists_range_iff.2 <| exists_rat_btwn h
#align rat.dense_range_cast Rat.denseRange_cast

namespace AddSubgroup

variable {G : Type*} [LinearOrderedAddCommGroup G] [TopologicalSpace G] [OrderTopology G]
  [Archimedean G]

/-- An additive subgroup of an archimedean linear ordered additive commutative group with order
topology is dense provided that for all positive `Îµ` there exists a positive element of the
subgroup that is less than `Îµ`. -/
theorem dense_of_not_isolated_zero (S : AddSubgroup G) (hS : âˆ€ Îµ > 0, âˆƒ g âˆˆ S, g âˆˆ Ioo 0 Îµ) :
    Dense (S : Set G) := by
  cases subsingleton_or_nontrivial G
  -- âŠ¢ Dense â†‘S
  Â· refine fun x => _root_.subset_closure ?_
    -- âŠ¢ x âˆˆ â†‘S
    rw [Subsingleton.elim x 0]
    -- âŠ¢ 0 âˆˆ â†‘S
    exact zero_mem S
    -- ğŸ‰ no goals
  refine dense_of_exists_between fun a b hlt => ?_
  -- âŠ¢ âˆƒ c, c âˆˆ â†‘S âˆ§ a < c âˆ§ c < b
  rcases hS (b - a) (sub_pos.2 hlt) with âŸ¨g, hgS, hg0, hgâŸ©
  -- âŠ¢ âˆƒ c, c âˆˆ â†‘S âˆ§ a < c âˆ§ c < b
  rcases (existsUnique_add_zsmul_mem_Ioc hg0 0 a).exists with âŸ¨m, hmâŸ©
  -- âŠ¢ âˆƒ c, c âˆˆ â†‘S âˆ§ a < c âˆ§ c < b
  rw [zero_add] at hm
  -- âŠ¢ âˆƒ c, c âˆˆ â†‘S âˆ§ a < c âˆ§ c < b
  refine âŸ¨m â€¢ g, zsmul_mem hgS _, hm.1, hm.2.trans_lt ?_âŸ©
  -- âŠ¢ a + g < b
  rwa [lt_sub_iff_add_lt'] at hg
  -- ğŸ‰ no goals

/-- Let `S` be a nontrivial additive subgroup in an archimedean linear ordered additive commutative
group `G` with order topology. If the set of positive elements of `S` does not have a minimal
element, then `S` is dense `G`. -/
theorem dense_of_no_min (S : AddSubgroup G) (hbot : S â‰  âŠ¥)
    (H : Â¬âˆƒ a : G, IsLeast { g : G | g âˆˆ S âˆ§ 0 < g } a) : Dense (S : Set G) := by
  refine S.dense_of_not_isolated_zero fun Îµ Îµ0 => ?_
  -- âŠ¢ âˆƒ g, g âˆˆ S âˆ§ g âˆˆ Ioo 0 Îµ
  contrapose! H
  -- âŠ¢ âˆƒ a, IsLeast {g | g âˆˆ S âˆ§ 0 < g} a
  exact exists_isLeast_pos hbot Îµ0 (disjoint_left.2 H)
  -- ğŸ‰ no goals
#align real.subgroup_dense_of_no_min AddSubgroup.dense_of_no_minâ‚“

/-- An additive subgroup of an archimedean linear ordered additive commutative group `G` with order
topology either is dense in `G` or is a cyclic subgroup. -/
theorem dense_or_cyclic (S : AddSubgroup G) : Dense (S : Set G) âˆ¨ âˆƒ a : G, S = closure {a} := by
  refine (em _).imp (dense_of_not_isolated_zero S) fun h => ?_
  -- âŠ¢ âˆƒ a, S = closure {a}
  push_neg at h
  -- âŠ¢ âˆƒ a, S = closure {a}
  rcases h with âŸ¨Îµ, Îµ0, hÎµâŸ©
  -- âŠ¢ âˆƒ a, S = closure {a}
  exact cyclic_of_isolated_zero Îµ0 (disjoint_left.2 hÎµ)
  -- ğŸ‰ no goals
#align real.subgroup_dense_or_cyclic AddSubgroup.dense_or_cyclicâ‚“
