/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.GroupTheory.Archimedean
import Mathlib.Topology.Algebra.Order.Group
import Mathlib.Algebra.Group.Subgroup.ZPowers.Basic
import Mathlib.Topology.Order.Basic

/-!
# Topology on archimedean groups and fields

In this file we prove the following theorems:

- `Rat.denseRange_cast`: the coercion from `â„š` to a linear ordered archimedean field has dense
  range;

- `AddSubgroup.dense_of_not_isolated_zero`, `AddSubgroup.dense_of_no_min`: two sufficient conditions
  for a subgroup of an archimedean linear ordered additive commutative group to be dense;

- `AddSubgroup.dense_or_cyclic`: an additive subgroup of an archimedean linear ordered additive
  commutative group `G` with order topology either is dense in `G` or is a cyclic subgroup.
-/

open Set

/-- Rational numbers are dense in a linear ordered archimedean field. -/
theorem Rat.denseRange_cast {ğ•œ} [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
    [TopologicalSpace ğ•œ] [OrderTopology ğ•œ]
    [Archimedean ğ•œ] : DenseRange ((â†‘) : â„š â†’ ğ•œ) :=
  dense_of_exists_between fun _ _ h â†¦ Set.exists_range_iff.2 <| exists_rat_btwn h

namespace Subgroup

variable {G : Type*} [CommGroup G] [LinearOrder G] [IsOrderedMonoid G]
  [TopologicalSpace G] [OrderTopology G]
  [MulArchimedean G]

/-- A subgroup of an archimedean linear ordered multiplicative commutative group with order
topology is dense provided that for all `Îµ > 1` there exists an element of the subgroup
that belongs to `(1, Îµ)`. -/
@[to_additive /-- An additive subgroup of an archimedean linear ordered additive commutative group
with order topology is dense provided that for all positive `Îµ` there exists a positive element of
the subgroup that is less than `Îµ`. -/]
theorem dense_of_not_isolated_one (S : Subgroup G) (hS : âˆ€ Îµ > 1, âˆƒ g âˆˆ S, g âˆˆ Ioo 1 Îµ) :
    Dense (S : Set G) := by
  cases subsingleton_or_nontrivial G
  Â· refine fun x â†¦ _root_.subset_closure ?_
    rw [Subsingleton.elim x 1]
    exact one_mem S
  refine dense_of_exists_between fun a b hlt â†¦ ?_
  rcases hS (b / a) (one_lt_div'.2 hlt) with âŸ¨g, hgS, hg0, hgâŸ©
  rcases (existsUnique_add_zpow_mem_Ioc hg0 1 a).exists with âŸ¨m, hmâŸ©
  rw [one_mul] at hm
  refine âŸ¨g ^ m, zpow_mem hgS _, hm.1, hm.2.trans_lt ?_âŸ©
  rwa [lt_div_iff_mul_lt'] at hg

/-- Let `S` be a nontrivial subgroup in an archimedean linear ordered multiplicative commutative
group `G` with order topology. If the set of elements of `S` that are greater than one
does not have a minimal element, then `S` is dense `G`. -/
@[to_additive /-- Let `S` be a nontrivial additive subgroup in an archimedean linear ordered
additive commutative group `G` with order topology. If the set of positive elements of `S` does not
have a minimal element, then `S` is dense `G`. -/]
theorem dense_of_no_min (S : Subgroup G) (hbot : S â‰  âŠ¥)
    (H : Â¬âˆƒ a : G, IsLeast { g : G | g âˆˆ S âˆ§ 1 < g } a) : Dense (S : Set G) := by
  refine S.dense_of_not_isolated_one fun Îµ Îµ1 â†¦ ?_
  contrapose! H
  exact exists_isLeast_one_lt hbot Îµ1 (disjoint_left.2 H)

/-- A subgroup of an archimedean linear ordered multiplicative commutative group `G` with order
topology either is dense in `G` or is a cyclic subgroup. -/
@[to_additive dense_or_cyclic
/-- An additive subgroup of an archimedean linear ordered additive commutative group `G`
with order topology either is dense in `G` or is a cyclic subgroup. -/]
theorem dense_or_cyclic (S : Subgroup G) : Dense (S : Set G) âˆ¨ âˆƒ a : G, S = closure {a} := by
  refine (em _).imp (dense_of_not_isolated_one S) fun h â†¦ ?_
  push_neg at h
  rcases h with âŸ¨Îµ, Îµ1, hÎµâŸ©
  exact cyclic_of_isolated_one Îµ1 (disjoint_left.2 hÎµ)

variable [Nontrivial G] [DenselyOrdered G]

/-- In a nontrivial densely linear ordered archimedean topological multiplicative group,
a subgroup is either dense or is cyclic, but not both.

For a non-exclusive `Or` version with weaker assumptions, see `Subgroup.dense_or_cyclic` above. -/
@[to_additive dense_xor'_cyclic
/-- In a nontrivial densely linear ordered archimedean topological additive group,
a subgroup is either dense or is cyclic, but not both.

For a non-exclusive `Or` version with weaker assumptions, see `AddSubgroup.dense_or_cyclic` above.
-/]
theorem dense_xor'_cyclic (s : Subgroup G) :
    Xor' (Dense (s : Set G)) (âˆƒ a, s = .zpowers a) := by
  if hd : Dense (s : Set G) then
    simp only [hd, xor_true]
    rintro âŸ¨a, rflâŸ©
    exact not_denseRange_zpow hd
  else
    simp only [hd, xor_false, id, zpowers_eq_closure]
    exact s.dense_or_cyclic.resolve_left hd

@[to_additive]
theorem dense_iff_ne_zpowers {s : Subgroup G} :
    Dense (s : Set G) â†” âˆ€ a, s â‰  .zpowers a := by
  simp [xor_iff_iff_not.1 s.dense_xor'_cyclic]

end Subgroup
