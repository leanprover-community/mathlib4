/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.GroupTheory.Archimedean
import Mathlib.GroupTheory.SpecificGroups.Cyclic
import Mathlib.Topology.Algebra.Order.Group
import Mathlib.Algebra.Group.Subgroup.ZPowers.Basic
import Mathlib.Topology.Order.DenselyOrdered
import Mathlib.Topology.DiscreteSubset
import Mathlib.Topology.Algebra.Group.Basic

/-!
# Topology on archimedean groups and fields

In this file we prove the following theorems:

- `Rat.denseRange_cast`: the coercion from `‚Ñö` to a linear ordered archimedean field has dense
  range;

- `AddSubgroup.dense_of_not_isolated_zero`, `AddSubgroup.dense_of_no_min`: two sufficient conditions
  for a subgroup of an archimedean linear ordered additive commutative group to be dense;

- `AddSubgroup.dense_or_cyclic`: an additive subgroup of an archimedean linear ordered additive
  commutative group `G` with order topology either is dense in `G` or is a cyclic subgroup.
-/

open Set

/-- Rational numbers are dense in a linear ordered archimedean field. -/
theorem Rat.denseRange_cast {ùïú} [Field ùïú] [LinearOrder ùïú] [IsStrictOrderedRing ùïú]
    [TopologicalSpace ùïú] [OrderTopology ùïú]
    [Archimedean ùïú] : DenseRange ((‚Üë) : ‚Ñö ‚Üí ùïú) :=
  dense_of_exists_between fun _ _ h => Set.exists_range_iff.2 <| exists_rat_btwn h

namespace Subgroup

variable {G : Type*} [CommGroup G] [LinearOrder G] [IsOrderedMonoid G]
  [TopologicalSpace G] [OrderTopology G]

/-- In a linearly ordered group with the order topology, the powers of a single element form a
discrete subgroup. -/
@[to_additive /-- In a linearly ordered additive group with the order topology, the multiples of a
single element form a discrete subgroup. -/]
instance instDiscreteTopologyZMultiples (g : G) : DiscreteTopology (zpowers g) := by
  wlog ha : 1 ‚â§ g
  ¬∑ specialize this g‚Åª¬π (one_le_inv'.mpr (le_of_not_ge ha))
    rwa [zpowers_inv] at this
  rcases eq_or_lt_of_le ha with rfl | ha
  ¬∑ rw [zpowers_one_eq_bot]
    exact Subsingleton.discreteTopology
  rw [discreteTopology_iff_isOpen_singleton_one, isOpen_induced_iff]
  refine ‚ü®Set.Ioo (g ^ (-1 : ‚Ñ§)) (g ^ (1 : ‚Ñ§)), isOpen_Ioo, ?_‚ü©
  ext ‚ü®_, ‚ü®n, rfl‚ü©‚ü©
  constructor
  ¬∑ simp only [Set.mem_preimage, Set.mem_Ioo, Set.mem_singleton_iff, and_imp]
    intro hn hn'
    rw [zpow_lt_zpow_iff_right ha] at hn hn'
    simp only [Subtype.ext_iff, show n = 0 by omega, zpow_zero, coe_one]
  ¬∑ simp_all

variable [MulArchimedean G]

/-- A subgroup of an archimedean linear ordered multiplicative commutative group with order
topology is dense provided that for all `Œµ > 1` there exists an element of the subgroup
that belongs to `(1, Œµ)`. -/
@[to_additive /-- An additive subgroup of an archimedean linear ordered additive commutative group
with order topology is dense provided that for all positive `Œµ` there exists a positive element of
the subgroup that is less than `Œµ`. -/]
theorem dense_of_not_isolated_one (S : Subgroup G) (hS : ‚àÄ Œµ > 1, ‚àÉ g ‚àà S, g ‚àà Ioo 1 Œµ) :
    Dense (S : Set G) := by
  cases subsingleton_or_nontrivial G
  ¬∑ refine fun x => _root_.subset_closure ?_
    rw [Subsingleton.elim x 1]
    exact one_mem S
  refine dense_of_exists_between fun a b hlt => ?_
  rcases hS (b / a) (one_lt_div'.2 hlt) with ‚ü®g, hgS, hg0, hg‚ü©
  rcases (existsUnique_add_zpow_mem_Ioc hg0 1 a).exists with ‚ü®m, hm‚ü©
  rw [one_mul] at hm
  refine ‚ü®g ^ m, zpow_mem hgS _, hm.1, hm.2.trans_lt ?_‚ü©
  rwa [lt_div_iff_mul_lt'] at hg

/-- Let `S` be a nontrivial subgroup in an archimedean linear ordered multiplicative commutative
group `G` with order topology. If the set of elements of `S` that are greater than one
does not have a minimal element, then `S` is dense `G`. -/
@[to_additive /-- Let `S` be a nontrivial additive subgroup in an archimedean linear ordered
additive commutative group `G` with order topology. If the set of positive elements of `S` does not
have a minimal element, then `S` is dense `G`. -/]
theorem dense_of_no_min (S : Subgroup G) (hbot : S ‚â† ‚ä•)
    (H : ¬¨‚àÉ a : G, IsLeast { g : G | g ‚àà S ‚àß 1 < g } a) : Dense (S : Set G) := by
  refine S.dense_of_not_isolated_one fun Œµ Œµ1 => ?_
  contrapose! H
  exact exists_isLeast_one_lt hbot Œµ1 (disjoint_left.2 H)

/-- A subgroup of an archimedean linear ordered multiplicative commutative group `G` with order
topology either is dense in `G` or is a cyclic subgroup. -/
@[to_additive dense_or_cyclic
/-- An additive subgroup of an archimedean linear ordered additive commutative group `G`
with order topology either is dense in `G` or is a cyclic subgroup. -/]
theorem dense_or_cyclic (S : Subgroup G) : Dense (S : Set G) ‚à® ‚àÉ a : G, S = closure {a} := by
  refine (em _).imp (dense_of_not_isolated_one S) fun h => ?_
  push_neg at h
  rcases h with ‚ü®Œµ, Œµ1, hŒµ‚ü©
  exact cyclic_of_isolated_one Œµ1 (disjoint_left.2 hŒµ)

variable [DenselyOrdered G]

/-- In an Archimedean densely linearly ordered group (with the order topology), a subgroup is
discrete iff it is cyclic. -/
@[to_additive /-- In an Archimedean densely linearly ordered additive group (with the order
topology), a subgroup is discrete iff it is cyclic. -/]
lemma discrete_iff_cyclic {H : Subgroup G} :
    IsCyclic H ‚Üî DiscreteTopology H := by
  rcases subsingleton_or_nontrivial G
  ¬∑ simp [isCyclic_of_subsingleton, Subsingleton.discreteTopology]
  rw [Subgroup.isCyclic_iff_exists_zpowers_eq_top]
  constructor
  ¬∑ rintro ‚ü®g, rfl‚ü©
    infer_instance
  ¬∑ have := H.dense_or_cyclic
    simp only [‚Üê Subgroup.zpowers_eq_closure, Eq.comm (a := H)] at this
    refine fun hA ‚Ü¶ this.resolve_left fun h ‚Ü¶ ?_
    -- remains to show a contradiction assuming `H` is both dense and discrete
    obtain ‚ü®U, hU‚ü© := discreteTopology_subtype_iff'.mp hA 1 (by simp)
    obtain ‚ü®j, hj‚ü© := mem_closure_iff.mp (h.diff_singleton 1 1) U hU.1
      (by simpa only [‚Üê Set.singleton_subset_iff, ‚Üê hU.2] using Set.inter_subset_left)
    grind

variable [Nontrivial G]

/-- In a nontrivial densely linear ordered archimedean topological multiplicative group,
a subgroup is either dense or is cyclic, but not both.

For a non-exclusive `Or` version with weaker assumptions, see `Subgroup.dense_or_cyclic` above. -/
@[to_additive dense_xor'_cyclic
/-- In a nontrivial densely linear ordered archimedean topological additive group,
a subgroup is either dense or is cyclic, but not both.

For a non-exclusive `Or` version with weaker assumptions, see `AddSubgroup.dense_or_cyclic` above.
-/]
theorem dense_xor'_cyclic (s : Subgroup G) :
    Xor' (Dense (s : Set G)) (‚àÉ a, s = .zpowers a) := by
  if hd : Dense (s : Set G) then
    simp only [hd, xor_true]
    rintro ‚ü®a, rfl‚ü©
    exact not_denseRange_zpow hd
  else
    simp only [hd, xor_false, id, zpowers_eq_closure]
    exact s.dense_or_cyclic.resolve_left hd

@[to_additive]
theorem dense_iff_ne_zpowers {s : Subgroup G} :
    Dense (s : Set G) ‚Üî ‚àÄ a, s ‚â† .zpowers a := by
  simp [xor_iff_iff_not.1 s.dense_xor'_cyclic]

end Subgroup
