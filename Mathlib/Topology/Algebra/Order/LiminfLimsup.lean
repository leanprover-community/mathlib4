/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Yury Kudryashov, YaÃ«l Dillies
-/
import Mathlib.Algebra.Order.Group.DenselyOrdered
import Mathlib.Data.Real.Archimedean
import Mathlib.Order.LiminfLimsup
import Mathlib.Topology.Order.Monotone
import Mathlib.Topology.Algebra.Group.Basic

/-!
# Lemmas about liminf and limsup in an order topology.

## Main declarations

* `BoundedLENhdsClass`: Typeclass stating that neighborhoods are eventually bounded above.
* `BoundedGENhdsClass`: Typeclass stating that neighborhoods are eventually bounded below.

## Implementation notes

The same lemmas are true in `â„`, `â„ Ã— â„`, `Î¹ â†’ â„`, `EuclideanSpace Î¹ â„`. To avoid code
duplication, we provide an ad hoc axiomatisation of the properties we need.
-/

open Filter TopologicalSpace
open scoped Topology

universe u v

variable {Î¹ Î± Î² R S : Type*} {Ï€ : Î¹ â†’ Type*}

/-- Ad hoc typeclass stating that neighborhoods are eventually bounded above. -/
class BoundedLENhdsClass (Î± : Type*) [Preorder Î±] [TopologicalSpace Î±] : Prop where
  isBounded_le_nhds (a : Î±) : (ğ“ a).IsBounded (Â· â‰¤ Â·)

/-- Ad hoc typeclass stating that neighborhoods are eventually bounded below. -/
class BoundedGENhdsClass (Î± : Type*) [Preorder Î±] [TopologicalSpace Î±] : Prop where
  isBounded_ge_nhds (a : Î±) : (ğ“ a).IsBounded (Â· â‰¥ Â·)

section Preorder
variable [Preorder Î±] [Preorder Î²] [TopologicalSpace Î±] [TopologicalSpace Î²]

section BoundedLENhdsClass
variable [BoundedLENhdsClass Î±] [BoundedLENhdsClass Î²] {f : Filter Î¹} {u : Î¹ â†’ Î±} {a : Î±}

theorem isBounded_le_nhds (a : Î±) : (ğ“ a).IsBounded (Â· â‰¤ Â·) :=
  BoundedLENhdsClass.isBounded_le_nhds _

theorem Filter.Tendsto.isBoundedUnder_le (h : Tendsto u f (ğ“ a)) : f.IsBoundedUnder (Â· â‰¤ Â·) u :=
  (isBounded_le_nhds a).mono h

theorem Filter.Tendsto.bddAbove_range_of_cofinite [IsDirected Î± (Â· â‰¤ Â·)]
    (h : Tendsto u cofinite (ğ“ a)) : BddAbove (Set.range u) :=
  h.isBoundedUnder_le.bddAbove_range_of_cofinite

theorem Filter.Tendsto.bddAbove_range [IsDirected Î± (Â· â‰¤ Â·)] {u : â„• â†’ Î±}
    (h : Tendsto u atTop (ğ“ a)) : BddAbove (Set.range u) :=
  h.isBoundedUnder_le.bddAbove_range

theorem isCobounded_ge_nhds (a : Î±) : (ğ“ a).IsCobounded (Â· â‰¥ Â·) :=
  (isBounded_le_nhds a).isCobounded_flip

theorem Filter.Tendsto.isCoboundedUnder_ge [NeBot f] (h : Tendsto u f (ğ“ a)) :
    f.IsCoboundedUnder (Â· â‰¥ Â·) u :=
  h.isBoundedUnder_le.isCobounded_flip

instance : BoundedGENhdsClass Î±áµ’áµˆ := âŸ¨@isBounded_le_nhds Î± _ _ _âŸ©

instance Prod.instBoundedLENhdsClass : BoundedLENhdsClass (Î± Ã— Î²) := by
  refine âŸ¨fun x â†¦ ?_âŸ©
  obtain âŸ¨a, haâŸ© := isBounded_le_nhds x.1
  obtain âŸ¨b, hbâŸ© := isBounded_le_nhds x.2
  rw [â† @Prod.mk.eta _ _ x, nhds_prod_eq]
  exact âŸ¨(a, b), ha.prod_mk hbâŸ©

instance Pi.instBoundedLENhdsClass [Finite Î¹] [âˆ€ i, Preorder (Ï€ i)] [âˆ€ i, TopologicalSpace (Ï€ i)]
    [âˆ€ i, BoundedLENhdsClass (Ï€ i)] : BoundedLENhdsClass (âˆ€ i, Ï€ i) := by
  refine âŸ¨fun x â†¦ ?_âŸ©
  rw [nhds_pi]
  choose f hf using fun i â†¦ isBounded_le_nhds (x i)
  exact âŸ¨f, eventually_pi hfâŸ©

end BoundedLENhdsClass

section BoundedGENhdsClass
variable [BoundedGENhdsClass Î±] [BoundedGENhdsClass Î²] {f : Filter Î¹} {u : Î¹ â†’ Î±} {a : Î±}

theorem isBounded_ge_nhds (a : Î±) : (ğ“ a).IsBounded (Â· â‰¥ Â·) :=
  BoundedGENhdsClass.isBounded_ge_nhds _

theorem Filter.Tendsto.isBoundedUnder_ge (h : Tendsto u f (ğ“ a)) : f.IsBoundedUnder (Â· â‰¥ Â·) u :=
  (isBounded_ge_nhds a).mono h

theorem Filter.Tendsto.bddBelow_range_of_cofinite [IsDirected Î± (Â· â‰¥ Â·)]
    (h : Tendsto u cofinite (ğ“ a)) : BddBelow (Set.range u) :=
  h.isBoundedUnder_ge.bddBelow_range_of_cofinite

theorem Filter.Tendsto.bddBelow_range [IsDirected Î± (Â· â‰¥ Â·)] {u : â„• â†’ Î±}
    (h : Tendsto u atTop (ğ“ a)) : BddBelow (Set.range u) :=
  h.isBoundedUnder_ge.bddBelow_range

theorem isCobounded_le_nhds (a : Î±) : (ğ“ a).IsCobounded (Â· â‰¤ Â·) :=
  (isBounded_ge_nhds a).isCobounded_flip

theorem Filter.Tendsto.isCoboundedUnder_le [NeBot f] (h : Tendsto u f (ğ“ a)) :
    f.IsCoboundedUnder (Â· â‰¤ Â·) u :=
  h.isBoundedUnder_ge.isCobounded_flip

instance : BoundedLENhdsClass Î±áµ’áµˆ := âŸ¨@isBounded_ge_nhds Î± _ _ _âŸ©

instance Prod.instBoundedGENhdsClass : BoundedGENhdsClass (Î± Ã— Î²) :=
  âŸ¨(Prod.instBoundedLENhdsClass (Î± := Î±áµ’áµˆ) (Î² := Î²áµ’áµˆ)).isBounded_le_nhdsâŸ©

instance Pi.instBoundedGENhdsClass [Finite Î¹] [âˆ€ i, Preorder (Ï€ i)] [âˆ€ i, TopologicalSpace (Ï€ i)]
    [âˆ€ i, BoundedGENhdsClass (Ï€ i)] : BoundedGENhdsClass (âˆ€ i, Ï€ i) :=
  âŸ¨(Pi.instBoundedLENhdsClass (Ï€ := fun i â†¦ (Ï€ i)áµ’áµˆ)).isBounded_le_nhdsâŸ©

end BoundedGENhdsClass

-- See note [lower instance priority]
instance (priority := 100) OrderTop.to_BoundedLENhdsClass [OrderTop Î±] : BoundedLENhdsClass Î± :=
  âŸ¨fun _a â†¦ isBounded_le_of_topâŸ©

-- See note [lower instance priority]
instance (priority := 100) OrderBot.to_BoundedGENhdsClass [OrderBot Î±] : BoundedGENhdsClass Î± :=
  âŸ¨fun _a â†¦ isBounded_ge_of_botâŸ©

end Preorder

-- See note [lower instance priority]
instance (priority := 100) BoundedLENhdsClass.of_closedIciTopology [LinearOrder Î±]
    [TopologicalSpace Î±] [ClosedIciTopology Î±] : BoundedLENhdsClass Î± :=
  âŸ¨fun a â†¦ ((isTop_or_exists_gt a).elim fun h â†¦ âŸ¨a, Eventually.of_forall hâŸ©) <|
    Exists.imp fun _b â†¦ eventually_le_nhdsâŸ©

-- See note [lower instance priority]
instance (priority := 100) BoundedGENhdsClass.of_closedIicTopology [LinearOrder Î±]
    [TopologicalSpace Î±] [ClosedIicTopology Î±] : BoundedGENhdsClass Î± :=
  inferInstanceAs <| BoundedGENhdsClass Î±áµ’áµˆáµ’áµˆ

section LiminfLimsup

section ConditionallyCompleteLinearOrder

variable [ConditionallyCompleteLinearOrder Î±] [TopologicalSpace Î±] [OrderTopology Î±]

/-- If the liminf and the limsup of a filter coincide, then this filter converges to
their common value, at least if the filter is eventually bounded above and below. -/
theorem le_nhds_of_limsSup_eq_limsInf {f : Filter Î±} {a : Î±} (hl : f.IsBounded (Â· â‰¤ Â·))
    (hg : f.IsBounded (Â· â‰¥ Â·)) (hs : f.limsSup = a) (hi : f.limsInf = a) : f â‰¤ ğ“ a :=
  tendsto_order.2 âŸ¨fun _ hb â†¦ gt_mem_sets_of_limsInf_gt hg <| hi.symm â–¸ hb,
    fun _ hb â†¦ lt_mem_sets_of_limsSup_lt hl <| hs.symm â–¸ hbâŸ©

theorem limsSup_nhds (a : Î±) : limsSup (ğ“ a) = a :=
  csInf_eq_of_forall_ge_of_forall_gt_exists_lt (isBounded_le_nhds a)
    (fun a' (h : { n : Î± | n â‰¤ a' } âˆˆ ğ“ a) â†¦ show a â‰¤ a' from @mem_of_mem_nhds _ _ a _ h)
    fun b (hba : a < b) â†¦
    show âˆƒ c, { n : Î± | n â‰¤ c } âˆˆ ğ“ a âˆ§ c < b from
      match dense_or_discrete a b with
      | Or.inl âŸ¨c, hac, hcbâŸ© => âŸ¨c, ge_mem_nhds hac, hcbâŸ©
      | Or.inr âŸ¨_, hâŸ© => âŸ¨a, (ğ“ a).sets_of_superset (gt_mem_nhds hba) h, hbaâŸ©

theorem limsInf_nhds (a : Î±) : limsInf (ğ“ a) = a :=
  limsSup_nhds (Î± := Î±áµ’áµˆ) a

/-- If a filter is converging, its limsup coincides with its limit. -/
theorem limsInf_eq_of_le_nhds {f : Filter Î±} {a : Î±} [NeBot f] (h : f â‰¤ ğ“ a) : f.limsInf = a :=
  have hb_ge : IsBounded (Â· â‰¥ Â·) f := (isBounded_ge_nhds a).mono h
  have hb_le : IsBounded (Â· â‰¤ Â·) f := (isBounded_le_nhds a).mono h
  le_antisymm
    (calc
      f.limsInf â‰¤ f.limsSup := limsInf_le_limsSup hb_le hb_ge
      _ â‰¤ (ğ“ a).limsSup := limsSup_le_limsSup_of_le h hb_ge.isCobounded_flip (isBounded_le_nhds a)
      _ = a := limsSup_nhds a)
    (calc
      a = (ğ“ a).limsInf := (limsInf_nhds a).symm
      _ â‰¤ f.limsInf := limsInf_le_limsInf_of_le h (isBounded_ge_nhds a) hb_le.isCobounded_flip)

/-- If a filter is converging, its liminf coincides with its limit. -/
theorem limsSup_eq_of_le_nhds {f : Filter Î±} {a : Î±} [NeBot f] (h : f â‰¤ ğ“ a) : f.limsSup = a :=
  limsInf_eq_of_le_nhds (Î± := Î±áµ’áµˆ) h

/-- If a function has a limit, then its limsup coincides with its limit. -/
theorem Filter.Tendsto.limsup_eq {f : Filter Î²} {u : Î² â†’ Î±} {a : Î±} [NeBot f]
    (h : Tendsto u f (ğ“ a)) : limsup u f = a :=
  limsSup_eq_of_le_nhds h

/-- If a function has a limit, then its liminf coincides with its limit. -/
theorem Filter.Tendsto.liminf_eq {f : Filter Î²} {u : Î² â†’ Î±} {a : Î±} [NeBot f]
    (h : Tendsto u f (ğ“ a)) : liminf u f = a :=
  limsInf_eq_of_le_nhds h

/-- If the liminf and the limsup of a function coincide, then the limit of the function
exists and has the same value. -/
theorem tendsto_of_liminf_eq_limsup {f : Filter Î²} {u : Î² â†’ Î±} {a : Î±} (hinf : liminf u f = a)
    (hsup : limsup u f = a) (h : f.IsBoundedUnder (Â· â‰¤ Â·) u := by isBoundedDefault)
    (h' : f.IsBoundedUnder (Â· â‰¥ Â·) u := by isBoundedDefault) : Tendsto u f (ğ“ a) :=
  le_nhds_of_limsSup_eq_limsInf h h' hsup hinf

/-- If a number `a` is less than or equal to the `liminf` of a function `f` at some filter
and is greater than or equal to the `limsup` of `f`, then `f` tends to `a` along this filter. -/
theorem tendsto_of_le_liminf_of_limsup_le {f : Filter Î²} {u : Î² â†’ Î±} {a : Î±} (hinf : a â‰¤ liminf u f)
    (hsup : limsup u f â‰¤ a) (h : f.IsBoundedUnder (Â· â‰¤ Â·) u := by isBoundedDefault)
    (h' : f.IsBoundedUnder (Â· â‰¥ Â·) u := by isBoundedDefault) : Tendsto u f (ğ“ a) := by
  rcases f.eq_or_neBot with rfl | _
  Â· exact tendsto_bot
  Â· exact tendsto_of_liminf_eq_limsup (le_antisymm (le_trans (liminf_le_limsup h h') hsup) hinf)
      (le_antisymm hsup (le_trans hinf (liminf_le_limsup h h'))) h h'

/-- Assume that, for any `a < b`, a sequence can not be infinitely many times below `a` and
above `b`. If it is also ultimately bounded above and below, then it has to converge. This even
works if `a` and `b` are restricted to a dense subset.
-/
theorem tendsto_of_no_upcrossings [DenselyOrdered Î±] {f : Filter Î²} {u : Î² â†’ Î±} {s : Set Î±}
    (hs : Dense s) (H : âˆ€ a âˆˆ s, âˆ€ b âˆˆ s, a < b â†’ Â¬((âˆƒá¶  n in f, u n < a) âˆ§ âˆƒá¶  n in f, b < u n))
    (h : f.IsBoundedUnder (Â· â‰¤ Â·) u := by isBoundedDefault)
    (h' : f.IsBoundedUnder (Â· â‰¥ Â·) u := by isBoundedDefault) :
    âˆƒ c : Î±, Tendsto u f (ğ“ c) := by
  rcases f.eq_or_neBot with rfl | hbot
  Â· exact âŸ¨sInf âˆ…, tendsto_botâŸ©
  refine âŸ¨limsup u f, ?_âŸ©
  apply tendsto_of_le_liminf_of_limsup_le _ le_rfl h h'
  by_contra! hlt
  obtain âŸ¨a, âŸ¨âŸ¨la, auâŸ©, asâŸ©âŸ© : âˆƒ a, (f.liminf u < a âˆ§ a < f.limsup u) âˆ§ a âˆˆ s :=
    dense_iff_inter_open.1 hs (Set.Ioo (f.liminf u) (f.limsup u)) isOpen_Ioo
      (Set.nonempty_Ioo.2 hlt)
  obtain âŸ¨b, âŸ¨âŸ¨ab, buâŸ©, bsâŸ©âŸ© : âˆƒ b, (a < b âˆ§ b < f.limsup u) âˆ§ b âˆˆ s :=
    dense_iff_inter_open.1 hs (Set.Ioo a (f.limsup u)) isOpen_Ioo (Set.nonempty_Ioo.2 au)
  have A : âˆƒá¶  n in f, u n < a := frequently_lt_of_liminf_lt (IsBounded.isCobounded_ge h) la
  have B : âˆƒá¶  n in f, b < u n := frequently_lt_of_lt_limsup (IsBounded.isCobounded_le h') bu
  exact H a as b bs ab âŸ¨A, BâŸ©

variable [FirstCountableTopology Î±] {f : Filter Î²} [CountableInterFilter f] {u : Î² â†’ Î±}

theorem eventually_le_limsup (hf : IsBoundedUnder (Â· â‰¤ Â·) f u := by isBoundedDefault) :
    âˆ€á¶  b in f, u b â‰¤ f.limsup u := by
  obtain ha | ha := isTop_or_exists_gt (f.limsup u)
  Â· exact Eventually.of_forall fun _ => ha _
  by_cases H : IsGLB (Set.Ioi (f.limsup u)) (f.limsup u)
  Â· obtain âŸ¨u, -, -, hua, huâŸ© := H.exists_seq_antitone_tendsto ha
    have := fun n => eventually_lt_of_limsup_lt (hu n) hf
    exact
      (eventually_countable_forall.2 this).mono fun b hb =>
        ge_of_tendsto hua <| Eventually.of_forall fun n => (hb _).le
  Â· obtain âŸ¨x, hx, xaâŸ© : âˆƒ x, (âˆ€ â¦ƒbâ¦„, f.limsup u < b â†’ x â‰¤ b) âˆ§ f.limsup u < x := by
      simp only [IsGLB, IsGreatest, lowerBounds, upperBounds, Set.mem_Ioi, Set.mem_setOf_eq,
        not_and, not_forall, not_le, exists_prop] at H
      exact H fun x => le_of_lt
    filter_upwards [eventually_lt_of_limsup_lt xa hf] with y hy
    contrapose! hy
    exact hx hy

theorem eventually_liminf_le (hf : IsBoundedUnder (Â· â‰¥ Â·) f u := by isBoundedDefault) :
    âˆ€á¶  b in f, f.liminf u â‰¤ u b :=
  eventually_le_limsup (Î± := Î±áµ’áµˆ) hf

end ConditionallyCompleteLinearOrder

section CompleteLinearOrder

variable [CompleteLinearOrder Î±] [TopologicalSpace Î±] [FirstCountableTopology Î±] [OrderTopology Î±]
  {f : Filter Î²} [CountableInterFilter f] {u : Î² â†’ Î±}

@[simp]
theorem limsup_eq_bot : f.limsup u = âŠ¥ â†” u =á¶ [f] âŠ¥ :=
  âŸ¨fun h =>
    (EventuallyLE.trans eventually_le_limsup <| Eventually.of_forall fun _ => h.le).mono fun _ hx =>
      le_antisymm hx bot_le,
    fun h => by
    rw [limsup_congr h]
    exact limsup_const_botâŸ©

@[simp]
theorem liminf_eq_top : f.liminf u = âŠ¤ â†” u =á¶ [f] âŠ¤ :=
  limsup_eq_bot (Î± := Î±áµ’áµˆ)

end CompleteLinearOrder

end LiminfLimsup

section Monotone

variable {F : Filter Î¹} [NeBot F]
  [ConditionallyCompleteLinearOrder R] [TopologicalSpace R] [OrderTopology R]
  [ConditionallyCompleteLinearOrder S] [TopologicalSpace S] [OrderTopology S]

/-- An antitone function between (conditionally) complete linear ordered spaces sends a
`Filter.limsSup` to the `Filter.liminf` of the image if the function is continuous at the `limsSup`
(and the filter is bounded from above and frequently bounded from below). -/
theorem Antitone.map_limsSup_of_continuousAt {F : Filter R} [NeBot F] {f : R â†’ S}
    (f_decr : Antitone f) (f_cont : ContinuousAt f F.limsSup)
    (bdd_above : F.IsBounded (Â· â‰¤ Â·) := by isBoundedDefault)
    (cobdd : F.IsCobounded (Â· â‰¤ Â·) := by isBoundedDefault) :
    f F.limsSup = F.liminf f := by
  apply le_antisymm
  Â· rw [limsSup, f_decr.map_csInf_of_continuousAt f_cont bdd_above cobdd]
    apply le_of_forall_lt
    intro c hc
    simp only [liminf, limsInf, eventually_map] at hc âŠ¢
    obtain âŸ¨d, hd, h'dâŸ© :=
      exists_lt_of_lt_csSup (bdd_above.recOn fun x hx â†¦ âŸ¨f x, Set.mem_image_of_mem f hxâŸ©) hc
    apply lt_csSup_of_lt ?_ ?_ h'd
    Â· simpa only [BddAbove, upperBounds]
        using Antitone.isCoboundedUnder_ge_of_isCobounded f_decr cobdd
    Â· rcases hd with âŸ¨e, âŸ¨he, fe_eq_dâŸ©âŸ©
      filter_upwards [he] with x hx using (fe_eq_d.symm â–¸ f_decr hx)
  Â· by_cases h' : âˆƒ c, c < F.limsSup âˆ§ Set.Ioo c F.limsSup = âˆ…
    Â· rcases h' with âŸ¨c, c_lt, hcâŸ©
      have B : âˆƒá¶  n in F, F.limsSup â‰¤ n := by
        apply (frequently_lt_of_lt_limsSup cobdd c_lt).mono
        intro x hx
        by_contra!
        have : (Set.Ioo c F.limsSup).Nonempty := âŸ¨x, âŸ¨hx, thisâŸ©âŸ©
        simp only [hc, Set.not_nonempty_empty] at this
      apply liminf_le_of_frequently_le _ (bdd_above.isBoundedUnder f_decr)
      exact B.mono fun x hx â†¦ f_decr hx
    push_neg at h'
    by_contra! H
    have not_bot : Â¬ IsBot F.limsSup := fun maybe_bot â†¦
      lt_irrefl (F.liminf f) <| lt_of_le_of_lt
        (liminf_le_of_frequently_le (Frequently.of_forall (fun r â†¦ f_decr (maybe_bot r)))
          (bdd_above.isBoundedUnder f_decr)) H
    obtain âŸ¨l, l_lt, h'lâŸ© :
        âˆƒ l < F.limsSup, Set.Ioc l F.limsSup âŠ† { x : R | f x < F.liminf f } := by
      apply exists_Ioc_subset_of_mem_nhds ((tendsto_order.1 f_cont.tendsto).2 _ H)
      simpa [IsBot] using not_bot
    obtain âŸ¨m, l_m, m_ltâŸ© : (Set.Ioo l F.limsSup).Nonempty := by
      contrapose! h'
      exact âŸ¨l, l_lt, h'âŸ©
    have B : F.liminf f â‰¤ f m := by
      apply liminf_le_of_frequently_le _ _
      Â· apply (frequently_lt_of_lt_limsSup cobdd m_lt).mono
        exact fun x hx â†¦ f_decr hx.le
      Â· exact IsBounded.isBoundedUnder f_decr bdd_above
    have I : f m < F.liminf f := h'l âŸ¨l_m, m_lt.leâŸ©
    exact lt_irrefl _ (B.trans_lt I)

/-- A continuous antitone function between (conditionally) complete linear ordered spaces sends a
`Filter.limsup` to the `Filter.liminf` of the images (if the filter is bounded from above and
frequently bounded from below). -/
theorem Antitone.map_limsup_of_continuousAt {f : R â†’ S} (f_decr : Antitone f) (a : Î¹ â†’ R)
    (f_cont : ContinuousAt f (F.limsup a))
    (bdd_above : F.IsBoundedUnder (Â· â‰¤ Â·) a := by isBoundedDefault)
    (cobdd : F.IsCoboundedUnder (Â· â‰¤ Â·) a := by isBoundedDefault) :
    f (F.limsup a) = F.liminf (f âˆ˜ a) :=
  f_decr.map_limsSup_of_continuousAt f_cont bdd_above cobdd

/-- An antitone function between (conditionally) complete linear ordered spaces sends a
`Filter.limsInf` to the `Filter.limsup` of the image if the function is continuous at the `limsInf`
(and the filter is bounded from below and frequently bounded from above). -/
theorem Antitone.map_limsInf_of_continuousAt {F : Filter R} [NeBot F] {f : R â†’ S}
    (f_decr : Antitone f) (f_cont : ContinuousAt f F.limsInf)
    (cobdd : F.IsCobounded (Â· â‰¥ Â·) := by isBoundedDefault)
    (bdd_below : F.IsBounded (Â· â‰¥ Â·) := by isBoundedDefault) : f F.limsInf = F.limsup f :=
  Antitone.map_limsSup_of_continuousAt (R := Ráµ’áµˆ) (S := Sáµ’áµˆ) f_decr.dual f_cont bdd_below cobdd

/-- A continuous antitone function between (conditionally) complete linear ordered spaces sends a
`Filter.liminf` to the `Filter.limsup` of the images (if the filter is bounded from below and
frequently bounded from above). -/
theorem Antitone.map_liminf_of_continuousAt {f : R â†’ S} (f_decr : Antitone f) (a : Î¹ â†’ R)
    (f_cont : ContinuousAt f (F.liminf a))
    (cobdd : F.IsCoboundedUnder (Â· â‰¥ Â·) a := by isBoundedDefault)
    (bdd_below : F.IsBoundedUnder (Â· â‰¥ Â·) a := by isBoundedDefault) :
    f (F.liminf a) = F.limsup (f âˆ˜ a) :=
  f_decr.map_limsInf_of_continuousAt f_cont cobdd bdd_below

/-- A monotone function between (conditionally) complete linear ordered spaces sends a
`Filter.limsSup` to the `Filter.limsup` of the image if the function is continuous at the `limsSup`
(and the filter is bounded from above and frequently bounded from below). -/
theorem Monotone.map_limsSup_of_continuousAt {F : Filter R} [NeBot F] {f : R â†’ S}
    (f_incr : Monotone f) (f_cont : ContinuousAt f F.limsSup)
    (bdd_above : F.IsBounded (Â· â‰¤ Â·) := by isBoundedDefault)
    (cobdd : F.IsCobounded (Â· â‰¤ Â·) := by isBoundedDefault) : f F.limsSup = F.limsup f :=
  Antitone.map_limsSup_of_continuousAt (S := Sáµ’áµˆ) f_incr f_cont bdd_above cobdd

/-- A continuous monotone function between (conditionally) complete linear ordered spaces sends a
`Filter.limsup` to the `Filter.limsup` of the images (if the filter is bounded from above and
frequently bounded from below). -/
theorem Monotone.map_limsup_of_continuousAt {f : R â†’ S} (f_incr : Monotone f) (a : Î¹ â†’ R)
    (f_cont : ContinuousAt f (F.limsup a))
    (bdd_above : F.IsBoundedUnder (Â· â‰¤ Â·) a := by isBoundedDefault)
    (cobdd : F.IsCoboundedUnder (Â· â‰¤ Â·) a := by isBoundedDefault) :
    f (F.limsup a) = F.limsup (f âˆ˜ a) :=
  f_incr.map_limsSup_of_continuousAt f_cont bdd_above cobdd

/-- A monotone function between (conditionally) complete linear ordered spaces sends a
`Filter.limsInf` to the `Filter.liminf` of the image if the function is continuous at the `limsInf`
(and the filter is bounded from below and frequently bounded from above). -/
theorem Monotone.map_limsInf_of_continuousAt {F : Filter R} [NeBot F] {f : R â†’ S}
    (f_incr : Monotone f) (f_cont : ContinuousAt f F.limsInf)
    (cobdd : F.IsCobounded (Â· â‰¥ Â·) := by isBoundedDefault)
    (bdd_below : F.IsBounded (Â· â‰¥ Â·) := by isBoundedDefault) : f F.limsInf = F.liminf f :=
  Antitone.map_limsSup_of_continuousAt (R := Ráµ’áµˆ) f_incr.dual f_cont bdd_below cobdd

/-- A continuous monotone function between (conditionally) complete linear ordered spaces sends a
`Filter.liminf` to the `Filter.liminf` of the images (if the filter is bounded from below and
frequently bounded from above). -/
theorem Monotone.map_liminf_of_continuousAt {f : R â†’ S} (f_incr : Monotone f) (a : Î¹ â†’ R)
    (f_cont : ContinuousAt f (F.liminf a))
    (cobdd : F.IsCoboundedUnder (Â· â‰¥ Â·) a := by isBoundedDefault)
    (bdd_below : F.IsBoundedUnder (Â· â‰¥ Â·) a := by isBoundedDefault) :
    f (F.liminf a) = F.liminf (f âˆ˜ a) :=
  f_incr.map_limsInf_of_continuousAt f_cont cobdd bdd_below

end Monotone

section LiminfLimsupAdd

variable [AddCommGroup Î±] [ConditionallyCompleteLinearOrder Î±] [DenselyOrdered Î±]
  [CovariantClass Î± Î± (fun a b â†¦ a + b) fun x1 x2 â†¦ x1 â‰¤ x2]
  {f : Filter Î¹} [f.NeBot] {u v : Î¹ â†’ Î±}

lemma le_limsup_add (hâ‚ : IsBoundedUnder (fun x1 x2 â†¦ x1 â‰¤ x2) f u := by isBoundedDefault)
    (hâ‚‚ : IsCoboundedUnder (fun x1 x2 â†¦ x1 â‰¤ x2) f u := by isBoundedDefault)
    (hâ‚ƒ : IsBoundedUnder (fun x1 x2 â†¦ x1 â‰¤ x2) f v := by isBoundedDefault)
    (hâ‚„ : IsBoundedUnder (fun x1 x2 â†¦ x1 â‰¥ x2) f v := by isBoundedDefault) :
    (limsup u f) + liminf v f â‰¤ limsup (u + v) f := by
  have h := isCoboundedUnder_le_add hâ‚„ hâ‚‚ -- These `have` tactic improve performance.
  have h' := isBoundedUnder_le_add hâ‚ƒ hâ‚
  rw [add_comm] at h h'
  refine add_le_of_forall_lt fun a a_u b b_v â†¦ (le_limsup_iff h h').2 fun c c_ab â†¦ ?_
  refine ((frequently_lt_of_lt_limsup hâ‚‚ a_u).and_eventually
    (eventually_lt_of_lt_liminf b_v hâ‚„)).mono fun _ ab_x â†¦ ?_
  exact c_ab.trans (add_lt_add ab_x.1 ab_x.2)

lemma limsup_add_le (hâ‚ : IsBoundedUnder (fun x1 x2 â†¦ x1 â‰¥ x2) f u := by isBoundedDefault)
    (hâ‚‚ : IsBoundedUnder (fun x1 x2 â†¦ x1 â‰¤ x2) f u := by isBoundedDefault)
    (hâ‚ƒ : IsCoboundedUnder (fun x1 x2 â†¦ x1 â‰¤ x2) f v := by isBoundedDefault)
    (hâ‚„ : IsBoundedUnder (fun x1 x2 â†¦ x1 â‰¤ x2) f v := by isBoundedDefault) :
    limsup (u + v) f â‰¤ (limsup u f) + limsup v f := by
  have h := isCoboundedUnder_le_add hâ‚ hâ‚ƒ
  have h' := isBoundedUnder_le_add hâ‚‚ hâ‚„
  refine le_add_of_forall_lt fun a a_u b b_v â†¦ ?_
  rw [limsup_le_iff h h']
  intro c c_ab
  filter_upwards [eventually_lt_of_limsup_lt a_u, eventually_lt_of_limsup_lt b_v] with x a_x b_x
  exact (add_lt_add a_x b_x).trans c_ab

lemma le_liminf_add (hâ‚ : IsBoundedUnder (fun x1 x2 â†¦ x1 â‰¥ x2) f u := by isBoundedDefault)
    (hâ‚‚ : IsBoundedUnder (fun x1 x2 â†¦ x1 â‰¤ x2) f u := by isBoundedDefault)
    (hâ‚ƒ : IsBoundedUnder (fun x1 x2 â†¦ x1 â‰¥ x2) f v := by isBoundedDefault)
    (hâ‚„ : IsCoboundedUnder (fun x1 x2 â†¦ x1 â‰¥ x2) f v := by isBoundedDefault) :
    (liminf u f) + liminf v f â‰¤ liminf (u + v) f := by
  have h := isCoboundedUnder_ge_add hâ‚‚ hâ‚„
  have h' := isBoundedUnder_ge_add hâ‚ hâ‚ƒ
  refine add_le_of_forall_lt fun a a_u b b_v â†¦ ?_
  rw [le_liminf_iff h h']
  intro c c_ab
  filter_upwards [eventually_lt_of_lt_liminf a_u, eventually_lt_of_lt_liminf b_v] with x a_x b_x
  exact c_ab.trans (add_lt_add a_x b_x)

lemma liminf_add_le (hâ‚ : IsBoundedUnder (fun x1 x2 â†¦ x1 â‰¥ x2) f u := by isBoundedDefault)
    (hâ‚‚ : IsBoundedUnder (fun x1 x2 â†¦ x1 â‰¤ x2) f u := by isBoundedDefault)
    (hâ‚ƒ : IsBoundedUnder (fun x1 x2 â†¦ x1 â‰¥ x2) f v := by isBoundedDefault)
    (hâ‚„ : IsCoboundedUnder (fun x1 x2 â†¦ x1 â‰¥ x2) f v := by isBoundedDefault) :
    liminf (u + v) f â‰¤ (limsup u f) + liminf v f := by
  have h := isCoboundedUnder_ge_add hâ‚‚ hâ‚„
  have h' := isBoundedUnder_ge_add hâ‚ hâ‚ƒ
  refine le_add_of_forall_lt fun a a_u b b_v â†¦ (liminf_le_iff h h').2 fun _ c_ab â†¦ ?_
  refine ((frequently_lt_of_liminf_lt hâ‚„ b_v).and_eventually
    (eventually_lt_of_limsup_lt a_u hâ‚‚)).mono fun _ ab_x â†¦ ?_
  exact (add_lt_add ab_x.2 ab_x.1).trans c_ab

end LiminfLimsupAdd

section LiminfLimsupMul

open Filter Real

variable {f : Filter Î¹} {u v : Î¹ â†’ â„}

lemma le_limsup_mul (hâ‚ : âˆƒá¶  x in f, 0 â‰¤ u x) (hâ‚‚ : IsBoundedUnder (fun x1 x2 â†¦ x1 â‰¤ x2) f u)
    (hâ‚ƒ : 0 â‰¤á¶ [f] v) (hâ‚„ : IsBoundedUnder (fun x1 x2 â†¦ x1 â‰¤ x2) f v) :
    (limsup u f) * liminf v f â‰¤ limsup (u * v) f := by
  have h := IsCoboundedUnder.of_frequently_ge (a := 0)
    <| (hâ‚.and_eventually hâ‚ƒ).mono fun x âŸ¨ux_0, vx_0âŸ© â†¦ mul_nonneg ux_0 vx_0
  have h' := isBoundedUnder_le_mul_of_nonneg hâ‚ hâ‚‚ hâ‚ƒ hâ‚„
  have u0 : 0 â‰¤ limsup u f := le_limsup_of_frequently_le hâ‚ hâ‚‚
  have uv : 0 â‰¤ limsup (u * v) f :=
    le_limsup_of_frequently_le ((hâ‚.and_eventually hâ‚ƒ).mono fun _ âŸ¨hu, hvâŸ© â†¦ mul_nonneg hu hv) h'
  refine mul_le_of_forall_lt_of_nonneg u0 uv fun a a0 au b b0 bv â†¦ ?_
  refine (le_limsup_iff h h').2 fun c c_ab â†¦ ?_
  replace hâ‚ := IsCoboundedUnder.of_frequently_ge hâ‚ -- Pre-compute it to gain 4 s.
  have hâ‚… := frequently_lt_of_lt_limsup hâ‚ au
  have hâ‚† := eventually_lt_of_lt_liminf bv (isBoundedUnder_of_eventually_ge hâ‚ƒ)
  apply (hâ‚….and_eventually (hâ‚†.and hâ‚ƒ)).mono
  exact fun x âŸ¨xa, âŸ¨xb, _âŸ©âŸ© â†¦ c_ab.trans_le <| mul_le_mul xa.le xb.le b0 (a0.trans xa.le)

lemma limsup_mul_le (hâ‚ : âˆƒá¶  x in f, 0 â‰¤ u x) (hâ‚‚ : IsBoundedUnder (fun x1 x2 â†¦ x1 â‰¤ x2) f u)
    (hâ‚ƒ : 0 â‰¤á¶ [f] v) (hâ‚„ : IsBoundedUnder (fun x1 x2 â†¦ x1 â‰¤ x2) f v) :
    limsup (u * v) f â‰¤ (limsup u f) * limsup v f := by
  have h := IsCoboundedUnder.of_frequently_ge (a := 0)
    <| (hâ‚.and_eventually hâ‚ƒ).mono fun x âŸ¨ux_0, vx_0âŸ© â†¦ mul_nonneg ux_0 vx_0
  have h' := isBoundedUnder_le_mul_of_nonneg hâ‚ hâ‚‚ hâ‚ƒ hâ‚„
  refine le_mul_of_forall_ltâ‚€ fun a a_u b b_v â†¦ (limsup_le_iff h h').2 fun c c_ab â†¦ ?_
  filter_upwards [eventually_lt_of_limsup_lt a_u, eventually_lt_of_limsup_lt b_v, hâ‚ƒ]
    with x x_a x_b v_0
  apply lt_of_le_of_lt _ c_ab
  rcases lt_or_ge (u x) 0 with u_0 | u_0
  Â· apply (mul_nonpos_of_nonpos_of_nonneg u_0.le v_0).trans
    exact mul_nonneg ((le_limsup_of_frequently_le hâ‚ hâ‚‚).trans a_u.le) (v_0.trans x_b.le)
  Â· exact mul_le_mul x_a.le x_b.le v_0 (u_0.trans x_a.le)

lemma le_liminf_mul [f.NeBot] (hâ‚ : 0 â‰¤á¶ [f] u) (hâ‚‚ : IsBoundedUnder (fun x1 x2 â†¦ x1 â‰¤ x2) f u)
    (hâ‚ƒ : 0 â‰¤á¶ [f] v) (hâ‚„ : IsCoboundedUnder (fun x1 x2 â†¦ x1 â‰¥ x2) f v) :
    (liminf u f) * liminf v f â‰¤ liminf (u * v) f := by
  have h := isCoboundedUnder_ge_mul_of_nonneg hâ‚ hâ‚‚ hâ‚ƒ hâ‚„
  have h' := isBoundedUnder_of_eventually_ge (a := 0)
    <| (hâ‚.and hâ‚ƒ).mono fun x âŸ¨u0, v0âŸ© â†¦ mul_nonneg u0 v0
  apply mul_le_of_forall_lt_of_nonneg (le_liminf_of_le hâ‚‚.isCoboundedUnder_ge hâ‚)
    (le_liminf_of_le h ((hâ‚.and hâ‚ƒ).mono fun x âŸ¨u0, v0âŸ© â†¦ mul_nonneg u0 v0))
  intro a a0 au b b0 bv
  refine (le_liminf_iff h h').2 fun c c_ab â†¦ ?_
  filter_upwards [eventually_lt_of_lt_liminf au (isBoundedUnder_of_eventually_ge hâ‚),
    eventually_lt_of_lt_liminf bv (isBoundedUnder_of_eventually_ge hâ‚ƒ)] with x xa xb
  exact c_ab.trans_le (mul_le_mul xa.le xb.le b0 (a0.trans xa.le))

lemma liminf_mul_le [f.NeBot] (hâ‚ : 0 â‰¤á¶ [f] u) (hâ‚‚ : IsBoundedUnder (fun x1 x2 â†¦ x1 â‰¤ x2) f u)
    (hâ‚ƒ : 0 â‰¤á¶ [f] v) (hâ‚„ : IsCoboundedUnder (fun x1 x2 â†¦ x1 â‰¥ x2) f v) :
    liminf (u * v) f â‰¤ (limsup u f) * liminf v f := by
  have h := isCoboundedUnder_ge_mul_of_nonneg hâ‚ hâ‚‚ hâ‚ƒ hâ‚„
  have h' := isBoundedUnder_of_eventually_ge (a := 0)
    <| (hâ‚.and hâ‚ƒ).mono fun x âŸ¨u_0, v_0âŸ© â†¦ mul_nonneg u_0 v_0
  refine le_mul_of_forall_ltâ‚€ fun a a_u b b_v â†¦ (liminf_le_iff h h').2 fun c c_ab â†¦ ?_
  refine ((frequently_lt_of_liminf_lt hâ‚„ b_v).and_eventually ((eventually_lt_of_limsup_lt a_u).and
    (hâ‚.and hâ‚ƒ))).mono fun x âŸ¨x_v, x_u, u_0, v_0âŸ© â†¦ ?_
  exact (mul_le_mul x_u.le x_v.le v_0 (u_0.trans x_u.le)).trans_lt c_ab

end LiminfLimsupMul
section LiminfLimsupAddSub
variable [ConditionallyCompleteLinearOrder R] [TopologicalSpace R] [OrderTopology R]

/-- `liminf (c + xáµ¢) = c + liminf xáµ¢`. -/
lemma limsup_const_add (F : Filter Î¹) [NeBot F] [Add R] [ContinuousAdd R]
    [AddLeftMono R] (f : Î¹ â†’ R) (c : R)
    (bdd_above : F.IsBoundedUnder (Â· â‰¤ Â·) f) (cobdd : F.IsCoboundedUnder (Â· â‰¤ Â·) f) :
    Filter.limsup (fun i â†¦ c + f i) F = c + Filter.limsup f F :=
  (Monotone.map_limsSup_of_continuousAt (F := F.map f) (f := fun (x : R) â†¦ c + x)
    (fun _ _ h â†¦ add_le_add_left h c) (continuous_add_left c).continuousAt bdd_above cobdd).symm

/-- `limsup (xáµ¢ + c) = (limsup xáµ¢) + c`. -/
lemma limsup_add_const (F : Filter Î¹) [NeBot F] [Add R] [ContinuousAdd R]
    [AddRightMono R] (f : Î¹ â†’ R) (c : R)
    (bdd_above : F.IsBoundedUnder (Â· â‰¤ Â·) f) (cobdd : F.IsCoboundedUnder (Â· â‰¤ Â·) f) :
    Filter.limsup (fun i â†¦ f i + c) F = Filter.limsup f F + c :=
  (Monotone.map_limsSup_of_continuousAt (F := F.map f) (f := fun (x : R) â†¦ x + c)
    (fun _ _ h â†¦ add_le_add_right h c) (continuous_add_right c).continuousAt bdd_above cobdd).symm

/-- `liminf (c + xáµ¢) = c + liminf xáµ¢`. -/
lemma liminf_const_add (F : Filter Î¹) [NeBot F] [Add R] [ContinuousAdd R]
    [AddLeftMono R] (f : Î¹ â†’ R) (c : R)
    (cobdd : F.IsCoboundedUnder (Â· â‰¥ Â·) f) (bdd_below : F.IsBoundedUnder (Â· â‰¥ Â·) f) :
    Filter.liminf (fun i â†¦ c + f i) F = c + Filter.liminf f F :=
  (Monotone.map_limsInf_of_continuousAt (F := F.map f) (f := fun (x : R) â†¦ c + x)
    (fun _ _ h â†¦ add_le_add_left h c) (continuous_add_left c).continuousAt cobdd bdd_below).symm

/-- `liminf (xáµ¢ + c) = (liminf xáµ¢) + c`. -/
lemma liminf_add_const (F : Filter Î¹) [NeBot F] [Add R] [ContinuousAdd R]
    [AddRightMono R] (f : Î¹ â†’ R) (c : R)
    (cobdd : F.IsCoboundedUnder (Â· â‰¥ Â·) f) (bdd_below : F.IsBoundedUnder (Â· â‰¥ Â·) f) :
    Filter.liminf (fun i â†¦ f i + c) F = Filter.liminf f F + c :=
  (Monotone.map_limsInf_of_continuousAt (F := F.map f) (f := fun (x : R) â†¦ x + c)
    (fun _ _ h â†¦ add_le_add_right h c) (continuous_add_right c).continuousAt cobdd bdd_below).symm

/-- `limsup (c - xáµ¢) = c - liminf xáµ¢`. -/
lemma limsup_const_sub (F : Filter Î¹) [AddCommSemigroup R] [Sub R] [ContinuousSub R] [OrderedSub R]
    [AddLeftMono R] (f : Î¹ â†’ R) (c : R)
    (cobdd : F.IsCoboundedUnder (Â· â‰¥ Â·) f) (bdd_below : F.IsBoundedUnder (Â· â‰¥ Â·) f) :
    Filter.limsup (fun i â†¦ c - f i) F = c - Filter.liminf f F := by
  rcases F.eq_or_neBot with rfl | _
  Â· simp only [liminf, limsInf, limsup, limsSup, map_bot, eventually_bot, Set.setOf_true]
    simp only [IsCoboundedUnder, IsCobounded, map_bot, eventually_bot, true_implies] at cobdd
    rcases cobdd with âŸ¨x, hxâŸ©
    refine (csInf_le ?_ (Set.mem_univ _)).antisymm
      (tsub_le_iff_tsub_le.1 (le_csSup ?_ (Set.mem_univ _)))
    Â· refine âŸ¨x - x, mem_lowerBounds.2 fun y â†¦ ?_âŸ©
      simp only [Set.mem_univ, true_implies]
      exact tsub_le_iff_tsub_le.1 (hx (x - y))
    Â· refine âŸ¨x, mem_upperBounds.2 fun y â†¦ ?_âŸ©
      simp only [Set.mem_univ, hx y, implies_true]
  Â· exact (Antitone.map_limsInf_of_continuousAt (F := F.map f) (f := fun (x : R) â†¦ c - x)
    (fun _ _ h â†¦ tsub_le_tsub_left h c) (continuous_sub_left c).continuousAt cobdd bdd_below).symm

/-- `limsup (xáµ¢ - c) = (limsup xáµ¢) - c`. -/
lemma limsup_sub_const (F : Filter Î¹) [AddCommSemigroup R] [Sub R] [ContinuousSub R] [OrderedSub R]
    (f : Î¹ â†’ R) (c : R)
    (bdd_above : F.IsBoundedUnder (Â· â‰¤ Â·) f) (cobdd : F.IsCoboundedUnder (Â· â‰¤ Â·) f) :
    Filter.limsup (fun i â†¦ f i - c) F = Filter.limsup f F - c := by
  rcases F.eq_or_neBot with rfl | _
  Â· have {a : R} : sInf Set.univ â‰¤ a := by
      apply csInf_le _ (Set.mem_univ a)
      simp only [IsCoboundedUnder, IsCobounded, map_bot, eventually_bot, true_implies] at cobdd
      rcases cobdd with âŸ¨x, hxâŸ©
      refine âŸ¨x, mem_lowerBounds.2 fun y â†¦ ?_âŸ©
      simp only [Set.mem_univ, hx y, implies_true]
    simp only [limsup, limsSup, map_bot, eventually_bot, Set.setOf_true]
    exact this.antisymm (tsub_le_iff_right.2 this)
  Â· apply (Monotone.map_limsSup_of_continuousAt (F := F.map f) (f := fun (x : R) â†¦ x - c) _ _).symm
    Â· exact fun _ _ h â†¦ tsub_le_tsub_right h c
    Â· exact (continuous_sub_right c).continuousAt

/-- `liminf (c - xáµ¢) = c - limsup xáµ¢`. -/
lemma liminf_const_sub (F : Filter Î¹) [NeBot F] [AddCommSemigroup R] [Sub R] [ContinuousSub R]
    [OrderedSub R] [AddLeftMono R] (f : Î¹ â†’ R) (c : R)
    (bdd_above : F.IsBoundedUnder (Â· â‰¤ Â·) f) (cobdd : F.IsCoboundedUnder (Â· â‰¤ Â·) f) :
    Filter.liminf (fun i â†¦ c - f i) F = c - Filter.limsup f F :=
  (Antitone.map_limsSup_of_continuousAt (F := F.map f) (f := fun (x : R) â†¦ c - x)
    (fun _ _ h â†¦ tsub_le_tsub_left h c) (continuous_sub_left c).continuousAt bdd_above cobdd).symm

/-- `liminf (xáµ¢ - c) = (liminf xáµ¢) - c`. -/
lemma liminf_sub_const (F : Filter Î¹) [NeBot F] [AddCommSemigroup R] [Sub R] [ContinuousSub R]
    [OrderedSub R] (f : Î¹ â†’ R) (c : R)
    (cobdd : F.IsCoboundedUnder (Â· â‰¥ Â·) f) (bdd_below : F.IsBoundedUnder (Â· â‰¥ Â·) f) :
    Filter.liminf (fun i â†¦ f i - c) F = Filter.liminf f F - c :=
  (Monotone.map_limsInf_of_continuousAt (F := F.map f) (f := fun (x : R) â†¦ x - c)
    (fun _ _ h â†¦ tsub_le_tsub_right h c) (continuous_sub_right c).continuousAt cobdd bdd_below).symm

end LiminfLimsupAddSub -- section
