/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Yury Kudryashov
-/
import Mathlib.Algebra.BigOperators.Intervals
import Mathlib.Algebra.BigOperators.Order
import Mathlib.Algebra.IndicatorFunction
import Mathlib.Topology.Algebra.Group.Basic
import Mathlib.Order.LiminfLimsup
import Mathlib.Order.Filter.Archimedean
import Mathlib.Order.Filter.CountableInter
import Mathlib.Topology.Order.Basic

#align_import topology.algebra.order.liminf_limsup from "leanprover-community/mathlib"@"52932b3a083d4142e78a15dc928084a22fea9ba0"

/-!
# Lemmas about liminf and limsup in an order topology.
-/


open Filter TopologicalSpace

open Topology Classical

universe u v

variable {Î± : Type u} {Î² : Type v}

section LiminfLimsup

section OrderClosedTopology

variable [SemilatticeSup Î±] [TopologicalSpace Î±] [OrderTopology Î±]

theorem isBounded_le_nhds (a : Î±) : (ğ“ a).IsBounded (Â· â‰¤ Â·) :=
  (isTop_or_exists_gt a).elim (fun h â†¦ âŸ¨a, eventually_of_forall hâŸ©) fun âŸ¨b, hbâŸ© â†¦
    âŸ¨b, ge_mem_nhds hbâŸ©
#align is_bounded_le_nhds isBounded_le_nhds

theorem Filter.Tendsto.isBoundedUnder_le {f : Filter Î²} {u : Î² â†’ Î±} {a : Î±}
    (h : Tendsto u f (ğ“ a)) : f.IsBoundedUnder (Â· â‰¤ Â·) u :=
  (isBounded_le_nhds a).mono h
#align filter.tendsto.is_bounded_under_le Filter.Tendsto.isBoundedUnder_le

theorem Filter.Tendsto.bddAbove_range_of_cofinite {u : Î² â†’ Î±} {a : Î±}
    (h : Tendsto u cofinite (ğ“ a)) : BddAbove (Set.range u) :=
  h.isBoundedUnder_le.bddAbove_range_of_cofinite
#align filter.tendsto.bdd_above_range_of_cofinite Filter.Tendsto.bddAbove_range_of_cofinite

theorem Filter.Tendsto.bddAbove_range {u : â„• â†’ Î±} {a : Î±} (h : Tendsto u atTop (ğ“ a)) :
    BddAbove (Set.range u) :=
  h.isBoundedUnder_le.bddAbove_range
#align filter.tendsto.bdd_above_range Filter.Tendsto.bddAbove_range

theorem isCobounded_ge_nhds (a : Î±) : (ğ“ a).IsCobounded (Â· â‰¥ Â·) :=
  (isBounded_le_nhds a).isCobounded_flip
#align is_cobounded_ge_nhds isCobounded_ge_nhds

theorem Filter.Tendsto.isCoboundedUnder_ge {f : Filter Î²} {u : Î² â†’ Î±} {a : Î±} [NeBot f]
    (h : Tendsto u f (ğ“ a)) : f.IsCoboundedUnder (Â· â‰¥ Â·) u :=
  h.isBoundedUnder_le.isCobounded_flip
#align filter.tendsto.is_cobounded_under_ge Filter.Tendsto.isCoboundedUnder_ge

theorem isBounded_le_atBot (Î± : Type*) [hÎ± : Nonempty Î±] [Preorder Î±] :
    (atBot : Filter Î±).IsBounded (Â· â‰¤ Â·) :=
  isBounded_iff.2 âŸ¨Set.Iic hÎ±.some, mem_atBot _, hÎ±.some, fun _ hx â†¦ hxâŸ©
#align is_bounded_le_at_bot isBounded_le_atBot

theorem Filter.Tendsto.isBoundedUnder_le_atBot {Î± : Type*} [Nonempty Î±] [Preorder Î±] {f : Filter Î²}
    {u : Î² â†’ Î±} (h : Tendsto u f atBot) : f.IsBoundedUnder (Â· â‰¤ Â·) u :=
  (isBounded_le_atBot Î±).mono h
#align filter.tendsto.is_bounded_under_le_at_bot Filter.Tendsto.isBoundedUnder_le_atBot

theorem bddAbove_range_of_tendsto_atTop_atBot {Î± : Type*} [Nonempty Î±] [SemilatticeSup Î±]
    {u : â„• â†’ Î±} (hx : Tendsto u atTop atBot) : BddAbove (Set.range u) :=
  (Filter.Tendsto.isBoundedUnder_le_atBot hx).bddAbove_range
#align bdd_above_range_of_tendsto_at_top_at_bot bddAbove_range_of_tendsto_atTop_atBot

end OrderClosedTopology

section OrderClosedTopology

variable [SemilatticeInf Î±] [TopologicalSpace Î±] [OrderTopology Î±]

theorem isBounded_ge_nhds (a : Î±) : (ğ“ a).IsBounded (Â· â‰¥ Â·) :=
  isBounded_le_nhds (Î± := Î±áµ’áµˆ) a
#align is_bounded_ge_nhds isBounded_ge_nhds

theorem Filter.Tendsto.isBoundedUnder_ge {f : Filter Î²} {u : Î² â†’ Î±} {a : Î±}
    (h : Tendsto u f (ğ“ a)) : f.IsBoundedUnder (Â· â‰¥ Â·) u :=
  (isBounded_ge_nhds a).mono h
#align filter.tendsto.is_bounded_under_ge Filter.Tendsto.isBoundedUnder_ge

theorem Filter.Tendsto.bddBelow_range_of_cofinite {u : Î² â†’ Î±} {a : Î±}
    (h : Tendsto u cofinite (ğ“ a)) : BddBelow (Set.range u) :=
  h.isBoundedUnder_ge.bddBelow_range_of_cofinite
#align filter.tendsto.bdd_below_range_of_cofinite Filter.Tendsto.bddBelow_range_of_cofinite

theorem Filter.Tendsto.bddBelow_range {u : â„• â†’ Î±} {a : Î±} (h : Tendsto u atTop (ğ“ a)) :
    BddBelow (Set.range u) :=
  h.isBoundedUnder_ge.bddBelow_range
#align filter.tendsto.bdd_below_range Filter.Tendsto.bddBelow_range

theorem isCobounded_le_nhds (a : Î±) : (ğ“ a).IsCobounded (Â· â‰¤ Â·) :=
  (isBounded_ge_nhds a).isCobounded_flip
#align is_cobounded_le_nhds isCobounded_le_nhds

theorem Filter.Tendsto.isCoboundedUnder_le {f : Filter Î²} {u : Î² â†’ Î±} {a : Î±} [NeBot f]
    (h : Tendsto u f (ğ“ a)) : f.IsCoboundedUnder (Â· â‰¤ Â·) u :=
  h.isBoundedUnder_ge.isCobounded_flip
#align filter.tendsto.is_cobounded_under_le Filter.Tendsto.isCoboundedUnder_le

theorem isBounded_ge_atTop (Î± : Type*) [Nonempty Î±] [Preorder Î±] :
    (atTop : Filter Î±).IsBounded (Â· â‰¥ Â·) :=
  isBounded_le_atBot Î±áµ’áµˆ
#align is_bounded_ge_at_top isBounded_ge_atTop

theorem Filter.Tendsto.isBoundedUnder_ge_atTop {Î± : Type*} [Nonempty Î±] [Preorder Î±] {f : Filter Î²}
    {u : Î² â†’ Î±} (h : Tendsto u f atTop) : f.IsBoundedUnder (Â· â‰¥ Â·) u :=
  (isBounded_ge_atTop Î±).mono h
#align filter.tendsto.is_bounded_under_ge_at_top Filter.Tendsto.isBoundedUnder_ge_atTop

theorem bddBelow_range_of_tendsto_atTop_atTop {Î± : Type*} [Nonempty Î±] [SemilatticeInf Î±]
    {u : â„• â†’ Î±} (hx : Tendsto u atTop atTop) : BddBelow (Set.range u) :=
  (Filter.Tendsto.isBoundedUnder_ge_atTop hx).bddBelow_range
#align bdd_below_range_of_tendsto_at_top_at_top bddBelow_range_of_tendsto_atTop_atTop

end OrderClosedTopology

section ConditionallyCompleteLinearOrder

variable [ConditionallyCompleteLinearOrder Î±]

theorem lt_mem_sets_of_limsSup_lt {f : Filter Î±} {b} (h : f.IsBounded (Â· â‰¤ Â·)) (l : f.limsSup < b) :
    âˆ€á¶  a in f, a < b :=
  let âŸ¨_, (h : âˆ€á¶  a in f, a â‰¤ _), hcbâŸ© := exists_lt_of_csInf_lt h l
  mem_of_superset h fun _ hac â†¦ lt_of_le_of_lt hac hcb
set_option linter.uppercaseLean3 false in
#align lt_mem_sets_of_Limsup_lt lt_mem_sets_of_limsSup_lt

theorem gt_mem_sets_of_limsInf_gt :
    âˆ€ {f : Filter Î±} {b}, f.IsBounded (Â· â‰¥ Â·) â†’ b < f.limsInf â†’ âˆ€á¶  a in f, b < a :=
  @lt_mem_sets_of_limsSup_lt Î±áµ’áµˆ _
set_option linter.uppercaseLean3 false in
#align gt_mem_sets_of_Liminf_gt gt_mem_sets_of_limsInf_gt

variable [TopologicalSpace Î±] [OrderTopology Î±]

/-- If the liminf and the limsup of a filter coincide, then this filter converges to
their common value, at least if the filter is eventually bounded above and below. -/
theorem le_nhds_of_limsSup_eq_limsInf {f : Filter Î±} {a : Î±} (hl : f.IsBounded (Â· â‰¤ Â·))
    (hg : f.IsBounded (Â· â‰¥ Â·)) (hs : f.limsSup = a) (hi : f.limsInf = a) : f â‰¤ ğ“ a :=
  tendsto_order.2 âŸ¨fun _ hb â†¦ gt_mem_sets_of_limsInf_gt hg <| hi.symm â–¸ hb,
    fun _ hb â†¦ lt_mem_sets_of_limsSup_lt hl <| hs.symm â–¸ hbâŸ©
set_option linter.uppercaseLean3 false in
#align le_nhds_of_Limsup_eq_Liminf le_nhds_of_limsSup_eq_limsInf

theorem limsSup_nhds (a : Î±) : limsSup (ğ“ a) = a :=
  csInf_eq_of_forall_ge_of_forall_gt_exists_lt (isBounded_le_nhds a)
    (fun a' (h : { n : Î± | n â‰¤ a' } âˆˆ ğ“ a) â†¦ show a â‰¤ a' from @mem_of_mem_nhds Î± _ a _ h)
    fun b (hba : a < b) â†¦
    show âˆƒ c, { n : Î± | n â‰¤ c } âˆˆ ğ“ a âˆ§ c < b from
      match dense_or_discrete a b with
      | Or.inl âŸ¨c, hac, hcbâŸ© => âŸ¨c, ge_mem_nhds hac, hcbâŸ©
      | Or.inr âŸ¨_, hâŸ© => âŸ¨a, (ğ“ a).sets_of_superset (gt_mem_nhds hba) h, hbaâŸ©
set_option linter.uppercaseLean3 false in
#align Limsup_nhds limsSup_nhds

theorem limsInf_nhds : âˆ€ a : Î±, limsInf (ğ“ a) = a :=
  limsSup_nhds (Î± := Î±áµ’áµˆ)
set_option linter.uppercaseLean3 false in
#align Liminf_nhds limsInf_nhds

/-- If a filter is converging, its limsup coincides with its limit. -/
theorem limsInf_eq_of_le_nhds {f : Filter Î±} {a : Î±} [NeBot f] (h : f â‰¤ ğ“ a) : f.limsInf = a :=
  have hb_ge : IsBounded (Â· â‰¥ Â·) f := (isBounded_ge_nhds a).mono h
  have hb_le : IsBounded (Â· â‰¤ Â·) f := (isBounded_le_nhds a).mono h
  le_antisymm
    (calc
      f.limsInf â‰¤ f.limsSup := limsInf_le_limsSup hb_le hb_ge
      _ â‰¤ (ğ“ a).limsSup := limsSup_le_limsSup_of_le h hb_ge.isCobounded_flip (isBounded_le_nhds a)
      _ = a := limsSup_nhds a)
    (calc
      a = (ğ“ a).limsInf := (limsInf_nhds a).symm
      _ â‰¤ f.limsInf := limsInf_le_limsInf_of_le h (isBounded_ge_nhds a) hb_le.isCobounded_flip)
set_option linter.uppercaseLean3 false in
#align Liminf_eq_of_le_nhds limsInf_eq_of_le_nhds

/-- If a filter is converging, its liminf coincides with its limit. -/
theorem limsSup_eq_of_le_nhds : âˆ€ {f : Filter Î±} {a : Î±} [NeBot f], f â‰¤ ğ“ a â†’ f.limsSup = a :=
  limsInf_eq_of_le_nhds (Î± := Î±áµ’áµˆ)
set_option linter.uppercaseLean3 false in
#align Limsup_eq_of_le_nhds limsSup_eq_of_le_nhds

/-- If a function has a limit, then its limsup coincides with its limit. -/
theorem Filter.Tendsto.limsup_eq {f : Filter Î²} {u : Î² â†’ Î±} {a : Î±} [NeBot f]
    (h : Tendsto u f (ğ“ a)) : limsup u f = a :=
  limsSup_eq_of_le_nhds h
#align filter.tendsto.limsup_eq Filter.Tendsto.limsup_eq

/-- If a function has a limit, then its liminf coincides with its limit. -/
theorem Filter.Tendsto.liminf_eq {f : Filter Î²} {u : Î² â†’ Î±} {a : Î±} [NeBot f]
    (h : Tendsto u f (ğ“ a)) : liminf u f = a :=
  limsInf_eq_of_le_nhds h
#align filter.tendsto.liminf_eq Filter.Tendsto.liminf_eq

/-- If the liminf and the limsup of a function coincide, then the limit of the function
exists and has the same value. -/
theorem tendsto_of_liminf_eq_limsup {f : Filter Î²} {u : Î² â†’ Î±} {a : Î±} (hinf : liminf u f = a)
    (hsup : limsup u f = a) (h : f.IsBoundedUnder (Â· â‰¤ Â·) u := by isBoundedDefault)
    (h' : f.IsBoundedUnder (Â· â‰¥ Â·) u := by isBoundedDefault) : Tendsto u f (ğ“ a) :=
  le_nhds_of_limsSup_eq_limsInf h h' hsup hinf
#align tendsto_of_liminf_eq_limsup tendsto_of_liminf_eq_limsup

/-- If a number `a` is less than or equal to the `liminf` of a function `f` at some filter
and is greater than or equal to the `limsup` of `f`, then `f` tends to `a` along this filter. -/
theorem tendsto_of_le_liminf_of_limsup_le {f : Filter Î²} {u : Î² â†’ Î±} {a : Î±} (hinf : a â‰¤ liminf u f)
    (hsup : limsup u f â‰¤ a) (h : f.IsBoundedUnder (Â· â‰¤ Â·) u := by isBoundedDefault)
    (h' : f.IsBoundedUnder (Â· â‰¥ Â·) u := by isBoundedDefault) : Tendsto u f (ğ“ a) :=
  if hf : f = âŠ¥ then hf.symm â–¸ tendsto_bot
  else
    haveI : NeBot f := âŸ¨hfâŸ©
    tendsto_of_liminf_eq_limsup (le_antisymm (le_trans (liminf_le_limsup h h') hsup) hinf)
      (le_antisymm hsup (le_trans hinf (liminf_le_limsup h h'))) h h'
#align tendsto_of_le_liminf_of_limsup_le tendsto_of_le_liminf_of_limsup_le

/-- Assume that, for any `a < b`, a sequence can not be infinitely many times below `a` and
above `b`. If it is also ultimately bounded above and below, then it has to converge. This even
works if `a` and `b` are restricted to a dense subset.
-/
theorem tendsto_of_no_upcrossings [DenselyOrdered Î±] {f : Filter Î²} {u : Î² â†’ Î±} {s : Set Î±}
    (hs : Dense s) (H : âˆ€ a âˆˆ s, âˆ€ b âˆˆ s, a < b â†’ Â¬((âˆƒá¶  n in f, u n < a) âˆ§ âˆƒá¶  n in f, b < u n))
    (h : f.IsBoundedUnder (Â· â‰¤ Â·) u := by isBoundedDefault)
    (h' : f.IsBoundedUnder (Â· â‰¥ Â·) u := by isBoundedDefault) :
    âˆƒ c : Î±, Tendsto u f (ğ“ c) := by
  rcases f.eq_or_neBot with rfl | hbot
  Â· exact âŸ¨sInf âˆ…, tendsto_botâŸ©
  refine' âŸ¨limsup u f, _âŸ©
  apply tendsto_of_le_liminf_of_limsup_le _ le_rfl h h'
  by_contra' hlt
  obtain âŸ¨a, âŸ¨âŸ¨la, auâŸ©, asâŸ©âŸ© : âˆƒ a, (f.liminf u < a âˆ§ a < f.limsup u) âˆ§ a âˆˆ s :=
    dense_iff_inter_open.1 hs (Set.Ioo (f.liminf u) (f.limsup u)) isOpen_Ioo
      (Set.nonempty_Ioo.2 hlt)
  obtain âŸ¨b, âŸ¨âŸ¨ab, buâŸ©, bsâŸ©âŸ© : âˆƒ b, (a < b âˆ§ b < f.limsup u) âˆ§ b âˆˆ s :=
    dense_iff_inter_open.1 hs (Set.Ioo a (f.limsup u)) isOpen_Ioo (Set.nonempty_Ioo.2 au)
  have A : âˆƒá¶  n in f, u n < a := frequently_lt_of_liminf_lt (IsBounded.isCobounded_ge h) la
  have B : âˆƒá¶  n in f, b < u n := frequently_lt_of_lt_limsup (IsBounded.isCobounded_le h') bu
  exact H a as b bs ab âŸ¨A, BâŸ©
#align tendsto_of_no_upcrossings tendsto_of_no_upcrossings

variable [FirstCountableTopology Î±] {f : Filter Î²} [CountableInterFilter f] {u : Î² â†’ Î±}

theorem eventually_le_limsup (hf : IsBoundedUnder (Â· â‰¤ Â·) f u := by isBoundedDefault) :
    âˆ€á¶  b in f, u b â‰¤ f.limsup u := by
  obtain ha | ha := isTop_or_exists_gt (f.limsup u)
  Â· exact eventually_of_forall fun _ => ha _
  by_cases H : IsGLB (Set.Ioi (f.limsup u)) (f.limsup u)
  Â· obtain âŸ¨u, -, -, hua, huâŸ© := H.exists_seq_antitone_tendsto ha
    have := fun n => eventually_lt_of_limsup_lt (hu n) hf
    exact
      (eventually_countable_forall.2 this).mono fun b hb =>
        ge_of_tendsto hua <| eventually_of_forall fun n => (hb _).le
  Â· obtain âŸ¨x, hx, xaâŸ© : âˆƒ x, (âˆ€ â¦ƒbâ¦„, f.limsup u < b â†’ x â‰¤ b) âˆ§ f.limsup u < x := by
      simp only [IsGLB, IsGreatest, lowerBounds, upperBounds, Set.mem_Ioi, Set.mem_setOf_eq,
        not_and, not_forall, not_le, exists_prop] at H
      exact H fun x => le_of_lt
    filter_upwards [eventually_lt_of_limsup_lt xa hf] with y hy
    contrapose! hy
    exact hx hy
#align eventually_le_limsup eventually_le_limsup

theorem eventually_liminf_le (hf : IsBoundedUnder (Â· â‰¥ Â·) f u := by isBoundedDefault) :
    âˆ€á¶  b in f, f.liminf u â‰¤ u b :=
  eventually_le_limsup (Î± := Î±áµ’áµˆ) hf
#align eventually_liminf_le eventually_liminf_le

end ConditionallyCompleteLinearOrder

section CompleteLinearOrder

variable [CompleteLinearOrder Î±] [TopologicalSpace Î±] [FirstCountableTopology Î±] [OrderTopology Î±]
  {f : Filter Î²} [CountableInterFilter f] {u : Î² â†’ Î±}

@[simp]
theorem limsup_eq_bot : f.limsup u = âŠ¥ â†” u =á¶ [f] âŠ¥ :=
  âŸ¨fun h =>
    (EventuallyLE.trans eventually_le_limsup <| eventually_of_forall fun _ => h.le).mono fun x hx =>
      le_antisymm hx bot_le,
    fun h => by
    rw [limsup_congr h]
    exact limsup_const_botâŸ©
#align limsup_eq_bot limsup_eq_bot

@[simp]
theorem liminf_eq_top : f.liminf u = âŠ¤ â†” u =á¶ [f] âŠ¤ :=
  limsup_eq_bot (Î± := Î±áµ’áµˆ)
#align liminf_eq_top liminf_eq_top

end CompleteLinearOrder

end LiminfLimsup

section Monotone

variable {Î¹ R S : Type*} {F : Filter Î¹} [NeBot F]
  [ConditionallyCompleteLinearOrder R] [TopologicalSpace R] [OrderTopology R]
  [ConditionallyCompleteLinearOrder S] [TopologicalSpace S] [OrderTopology S]

/-- An antitone function between (conditionally) complete linear ordered spaces sends a
`Filter.limsSup` to the `Filter.liminf` of the image if the function is continuous at the `limsSup`
(and the filter is bounded from above and below). -/
theorem Antitone.map_limsSup_of_continuousAt {F : Filter R} [NeBot F] {f : R â†’ S}
    (f_decr : Antitone f) (f_cont : ContinuousAt f F.limsSup)
    (bdd_above : F.IsBounded (Â· â‰¤ Â·) := by isBoundedDefault)
    (bdd_below : F.IsBounded (Â· â‰¥ Â·) := by isBoundedDefault) :
    f F.limsSup = F.liminf f := by
  have cobdd : F.IsCobounded (Â· â‰¤ Â·) := bdd_below.isCobounded_flip
  apply le_antisymm
  Â· rw [limsSup, f_decr.map_sInf_of_continuousAt' f_cont bdd_above cobdd]
    apply le_of_forall_lt
    intro c hc
    simp only [liminf, limsInf, eventually_map] at hc âŠ¢
    obtain âŸ¨d, hd, h'dâŸ© :=
      exists_lt_of_lt_csSup (bdd_above.recOn fun x hx â†¦ âŸ¨f x, Set.mem_image_of_mem f hxâŸ©) hc
    apply lt_csSup_of_lt ?_ ?_ h'd
    Â· exact (Antitone.isBoundedUnder_le_comp f_decr bdd_below).isCoboundedUnder_flip
    Â· rcases hd with âŸ¨e, âŸ¨he, fe_eq_dâŸ©âŸ©
      filter_upwards [he] with x hx using (fe_eq_d.symm â–¸ f_decr hx)
  Â· by_cases h' : âˆƒ c, c < F.limsSup âˆ§ Set.Ioo c F.limsSup = âˆ…
    Â· rcases h' with âŸ¨c, c_lt, hcâŸ©
      have B : âˆƒá¶  n in F, F.limsSup â‰¤ n := by
        apply (frequently_lt_of_lt_limsSup cobdd c_lt).mono
        intro x hx
        by_contra'
        have : (Set.Ioo c F.limsSup).Nonempty := âŸ¨x, âŸ¨hx, thisâŸ©âŸ©
        simp only [hc, Set.not_nonempty_empty] at this
      apply liminf_le_of_frequently_le _ (bdd_above.isBoundedUnder f_decr)
      exact (B.mono fun x hx â†¦ f_decr hx)
    push_neg at h'
    by_contra' H
    have not_bot : Â¬ IsBot F.limsSup := fun maybe_bot â†¦
      lt_irrefl (F.liminf f) <| lt_of_le_of_lt
        (liminf_le_of_frequently_le (frequently_of_forall (fun r â†¦ f_decr (maybe_bot r)))
          (bdd_above.isBoundedUnder f_decr)) H
    obtain âŸ¨l, l_lt, h'lâŸ© : âˆƒ l < F.limsSup, Set.Ioc l F.limsSup âŠ† { x : R | f x < F.liminf f }
    Â· apply exists_Ioc_subset_of_mem_nhds ((tendsto_order.1 f_cont.tendsto).2 _ H)
      simpa [IsBot] using not_bot
    obtain âŸ¨m, l_m, m_ltâŸ© : (Set.Ioo l F.limsSup).Nonempty := by
      contrapose! h'
      refine' âŸ¨l, l_lt, by rwa [Set.not_nonempty_iff_eq_empty] at h'âŸ©
    have B : F.liminf f â‰¤ f m := by
      apply liminf_le_of_frequently_le _ _
      Â· apply (frequently_lt_of_lt_limsSup cobdd m_lt).mono
        exact fun x hx â†¦ f_decr hx.le
      Â· exact IsBounded.isBoundedUnder f_decr bdd_above
    have I : f m < F.liminf f := h'l âŸ¨l_m, m_lt.leâŸ©
    exact lt_irrefl _ (B.trans_lt I)
set_option linter.uppercaseLean3 false in
#align antitone.map_Limsup_of_continuous_at Antitone.map_limsSup_of_continuousAt

/-- A continuous antitone function between (conditionally) complete linear ordered spaces sends a
`Filter.limsup` to the `Filter.liminf` of the images (if the filter is bounded from above and
below). -/
theorem Antitone.map_limsup_of_continuousAt {f : R â†’ S} (f_decr : Antitone f) (a : Î¹ â†’ R)
    (f_cont : ContinuousAt f (F.limsup a))
    (bdd_above : F.IsBoundedUnder (Â· â‰¤ Â·) a := by isBoundedDefault)
    (bdd_below : F.IsBoundedUnder (Â· â‰¥ Â·) a := by isBoundedDefault) :
    f (F.limsup a) = F.liminf (f âˆ˜ a) :=
  f_decr.map_limsSup_of_continuousAt f_cont bdd_above bdd_below
#align antitone.map_limsup_of_continuous_at Antitone.map_limsup_of_continuousAt

/-- An antitone function between (conditionally) complete linear ordered spaces sends a
`Filter.limsInf` to the `Filter.limsup` of the image if the function is continuous at the `limsInf`
(and the filter is bounded from above and below). -/
theorem Antitone.map_limsInf_of_continuousAt {F : Filter R} [NeBot F] {f : R â†’ S}
    (f_decr : Antitone f) (f_cont : ContinuousAt f F.limsInf)
    (bdd_above : F.IsBounded (Â· â‰¤ Â·) := by isBoundedDefault)
    (bdd_below : F.IsBounded (Â· â‰¥ Â·) := by isBoundedDefault) : f F.limsInf = F.limsup f :=
  Antitone.map_limsSup_of_continuousAt (R := Ráµ’áµˆ) (S := Sáµ’áµˆ) f_decr.dual f_cont bdd_below bdd_above
set_option linter.uppercaseLean3 false in
#align antitone.map_Liminf_of_continuous_at Antitone.map_limsInf_of_continuousAt

/-- A continuous antitone function between (conditionally) complete linear ordered spaces sends a
`Filter.liminf` to the `Filter.limsup` of the images (if the filter is bounded from above and
below). -/
theorem Antitone.map_liminf_of_continuousAt {f : R â†’ S} (f_decr : Antitone f) (a : Î¹ â†’ R)
    (f_cont : ContinuousAt f (F.liminf a))
    (bdd_above : F.IsBoundedUnder (Â· â‰¤ Â·) a := by isBoundedDefault)
    (bdd_below : F.IsBoundedUnder (Â· â‰¥ Â·) a := by isBoundedDefault) :
    f (F.liminf a) = F.limsup (f âˆ˜ a) :=
  f_decr.map_limsInf_of_continuousAt f_cont bdd_above bdd_below
#align antitone.map_liminf_of_continuous_at Antitone.map_liminf_of_continuousAt

/-- A monotone function between (conditionally) complete linear ordered spaces sends a
`Filter.limsSup` to the `Filter.limsup` of the image if the function is continuous at the `limsSup`
(and the filter is bounded from above and below). -/
theorem Monotone.map_limsSup_of_continuousAt {F : Filter R} [NeBot F] {f : R â†’ S}
    (f_incr : Monotone f) (f_cont : ContinuousAt f F.limsSup)
    (bdd_above : F.IsBounded (Â· â‰¤ Â·) := by isBoundedDefault)
    (bdd_below : F.IsBounded (Â· â‰¥ Â·) := by isBoundedDefault) : f F.limsSup = F.limsup f :=
  Antitone.map_limsSup_of_continuousAt (S := Sáµ’áµˆ) f_incr f_cont bdd_above bdd_below
set_option linter.uppercaseLean3 false in
#align monotone.map_Limsup_of_continuous_at Monotone.map_limsSup_of_continuousAt

/-- A continuous monotone function between (conditionally) complete linear ordered spaces sends a
`Filter.limsup` to the `Filter.limsup` of the images (if the filter is bounded from above and
below). -/
theorem Monotone.map_limsup_of_continuousAt {f : R â†’ S} (f_incr : Monotone f) (a : Î¹ â†’ R)
    (f_cont : ContinuousAt f (F.limsup a))
    (bdd_above : F.IsBoundedUnder (Â· â‰¤ Â·) a := by isBoundedDefault)
    (bdd_below : F.IsBoundedUnder (Â· â‰¥ Â·) a := by isBoundedDefault) :
    f (F.limsup a) = F.limsup (f âˆ˜ a) :=
  f_incr.map_limsSup_of_continuousAt f_cont bdd_above bdd_below
#align monotone.map_limsup_of_continuous_at Monotone.map_limsup_of_continuousAt

/-- A monotone function between (conditionally) complete linear ordered spaces sends a
`Filter.limsInf` to the `Filter.liminf` of the image if the function is continuous at the `limsInf`
(and the filter is bounded from above and below). -/
theorem Monotone.map_limsInf_of_continuousAt {F : Filter R} [NeBot F] {f : R â†’ S}
    (f_incr : Monotone f) (f_cont : ContinuousAt f F.limsInf)
    (bdd_above : F.IsBounded (Â· â‰¤ Â·) := by isBoundedDefault)
    (bdd_below : F.IsBounded (Â· â‰¥ Â·) := by isBoundedDefault) : f F.limsInf = F.liminf f :=
  Antitone.map_limsSup_of_continuousAt (R := Ráµ’áµˆ) f_incr.dual f_cont bdd_below bdd_above
set_option linter.uppercaseLean3 false in
#align monotone.map_Liminf_of_continuous_at Monotone.map_limsInf_of_continuousAt

/-- A continuous monotone function between (conditionally) complete linear ordered spaces sends a
`Filter.liminf` to the `Filter.liminf` of the images (if the filter is bounded from above and
below). -/
theorem Monotone.map_liminf_of_continuousAt {f : R â†’ S} (f_incr : Monotone f) (a : Î¹ â†’ R)
    (f_cont : ContinuousAt f (F.liminf a))
    (bdd_above : F.IsBoundedUnder (Â· â‰¤ Â·) a := by isBoundedDefault)
    (bdd_below : F.IsBoundedUnder (Â· â‰¥ Â·) a := by isBoundedDefault) :
    f (F.liminf a) = F.liminf (f âˆ˜ a) :=
  f_incr.map_limsInf_of_continuousAt f_cont bdd_above bdd_below
#align monotone.map_liminf_of_continuous_at Monotone.map_liminf_of_continuousAt

end Monotone

section InfiAndSupr

open Topology

open Filter Set

variable {Î¹ : Type*} {R : Type*} [CompleteLinearOrder R] [TopologicalSpace R] [OrderTopology R]

theorem iInf_eq_of_forall_le_of_tendsto {x : R} {as : Î¹ â†’ R} (x_le : âˆ€ i, x â‰¤ as i) {F : Filter Î¹}
    [Filter.NeBot F] (as_lim : Filter.Tendsto as F (ğ“ x)) : â¨… i, as i = x := by
  refine' iInf_eq_of_forall_ge_of_forall_gt_exists_lt (fun i â†¦ x_le i) _
  apply fun w x_lt_w â†¦ â€¹Filter.NeBot Fâ€º.nonempty_of_mem (eventually_lt_of_tendsto_lt x_lt_w as_lim)
#align infi_eq_of_forall_le_of_tendsto iInf_eq_of_forall_le_of_tendsto

theorem iSup_eq_of_forall_le_of_tendsto {x : R} {as : Î¹ â†’ R} (le_x : âˆ€ i, as i â‰¤ x) {F : Filter Î¹}
    [Filter.NeBot F] (as_lim : Filter.Tendsto as F (ğ“ x)) : â¨† i, as i = x :=
  iInf_eq_of_forall_le_of_tendsto (R := Ráµ’áµˆ) le_x as_lim
#align supr_eq_of_forall_le_of_tendsto iSup_eq_of_forall_le_of_tendsto

theorem iUnion_Ici_eq_Ioi_of_lt_of_tendsto {Î¹ : Type*} (x : R) {as : Î¹ â†’ R} (x_lt : âˆ€ i, x < as i)
    {F : Filter Î¹} [Filter.NeBot F] (as_lim : Filter.Tendsto as F (ğ“ x)) :
    â‹ƒ i : Î¹, Ici (as i) = Ioi x := by
  have obs : x âˆ‰ range as := by
    intro maybe_x_is
    rcases mem_range.mp maybe_x_is with âŸ¨i, hiâŸ©
    simpa only [hi, lt_self_iff_false] using x_lt i
  -- Porting note: `rw at *` was too destructive. Let's only rewrite `obs` and the goal.
  have := iInf_eq_of_forall_le_of_tendsto (fun i â†¦ (x_lt i).le) as_lim
  rw [â† this] at obs
  rw [â† this]
  exact iUnion_Ici_eq_Ioi_iInf obs
#align Union_Ici_eq_Ioi_of_lt_of_tendsto iUnion_Ici_eq_Ioi_of_lt_of_tendsto

theorem iUnion_Iic_eq_Iio_of_lt_of_tendsto {Î¹ : Type*} (x : R) {as : Î¹ â†’ R} (lt_x : âˆ€ i, as i < x)
    {F : Filter Î¹} [Filter.NeBot F] (as_lim : Filter.Tendsto as F (ğ“ x)) :
    â‹ƒ i : Î¹, Iic (as i) = Iio x :=
  iUnion_Ici_eq_Ioi_of_lt_of_tendsto (R := Ráµ’áµˆ) x lt_x as_lim
#align Union_Iic_eq_Iio_of_lt_of_tendsto iUnion_Iic_eq_Iio_of_lt_of_tendsto

end InfiAndSupr

section Indicator

open BigOperators

theorem limsup_eq_tendsto_sum_indicator_nat_atTop (s : â„• â†’ Set Î±) :
    limsup s atTop = { Ï‰ | Tendsto
      (fun n â†¦ âˆ‘ k in Finset.range n, (s (k + 1)).indicator (1 : Î± â†’ â„•) Ï‰) atTop atTop } := by
  ext Ï‰
  simp only [limsup_eq_iInf_iSup_of_nat, ge_iff_le, Set.iSup_eq_iUnion, Set.iInf_eq_iInter,
    Set.mem_iInter, Set.mem_iUnion, exists_prop]
  constructor
  Â· intro hÏ‰
    refine' tendsto_atTop_atTop_of_monotone' (fun n m hnm â†¦ Finset.sum_mono_set_of_nonneg
      (fun i â†¦ Set.indicator_nonneg (fun _ _ â†¦ zero_le_one) _) (Finset.range_mono hnm)) _
    rintro âŸ¨i, hâŸ©
    simp only [mem_upperBounds, Set.mem_range, forall_exists_index, forall_apply_eq_imp_iff'] at h
    induction' i with k hk
    Â· obtain âŸ¨j, hjâ‚, hjâ‚‚âŸ© := hÏ‰ 1
      refine' not_lt.2 (h <| j + 1)
        (lt_of_le_of_lt (Finset.sum_const_zero.symm : 0 = âˆ‘ k in Finset.range (j + 1), 0).le _)
      refine' Finset.sum_lt_sum (fun m _ â†¦ Set.indicator_nonneg (fun _ _ â†¦ zero_le_one) _)
        âŸ¨j - 1, Finset.mem_range.2 (lt_of_le_of_lt (Nat.sub_le _ _) j.lt_succ_self), _âŸ©
      rw [Nat.sub_add_cancel hjâ‚, Set.indicator_of_mem hjâ‚‚]
      exact zero_lt_one
    Â· rw [imp_false] at hk
      push_neg at hk
      obtain âŸ¨i, hiâŸ© := hk
      obtain âŸ¨j, hjâ‚, hjâ‚‚âŸ© := hÏ‰ (i + 1)
      replace hi : (âˆ‘ k in Finset.range i, (s (k + 1)).indicator 1 Ï‰) = k + 1 :=
        le_antisymm (h i) hi
      refine' not_lt.2 (h <| j + 1) _
      rw [â† Finset.sum_range_add_sum_Ico _ (i.le_succ.trans (hjâ‚.trans j.le_succ)), hi]
      refine' lt_add_of_pos_right _ _
      rw [(Finset.sum_const_zero.symm : 0 = âˆ‘ k in Finset.Ico i (j + 1), 0)]
      refine' Finset.sum_lt_sum (fun m _ â†¦ Set.indicator_nonneg (fun _ _ â†¦ zero_le_one) _)
        âŸ¨j - 1, Finset.mem_Ico.2 âŸ¨(Nat.le_sub_iff_add_le (le_trans ((le_add_iff_nonneg_left _).2
          zero_le') hjâ‚)).2 hjâ‚, lt_of_le_of_lt (Nat.sub_le _ _) j.lt_succ_selfâŸ©, _âŸ©
      rw [Nat.sub_add_cancel (le_trans ((le_add_iff_nonneg_left _).2 zero_le') hjâ‚),
        Set.indicator_of_mem hjâ‚‚]
      exact zero_lt_one
  Â· rintro hÏ‰ i
    rw [Set.mem_setOf_eq, tendsto_atTop_atTop] at hÏ‰
    by_contra hcon
    push_neg at hcon
    obtain âŸ¨j, hâŸ© := hÏ‰ (i + 1)
    have : (âˆ‘ k in Finset.range j, (s (k + 1)).indicator 1 Ï‰) â‰¤ i := by
      have hle : âˆ€ j â‰¤ i, (âˆ‘ k in Finset.range j, (s (k + 1)).indicator 1 Ï‰) â‰¤ i := by
        refine' fun j hij â†¦
          (Finset.sum_le_card_nsmul _ _ _ _ : _ â‰¤ (Finset.range j).card â€¢ 1).trans _
        Â· exact fun m _ â†¦ Set.indicator_apply_le' (fun _ â†¦ le_rfl) fun _ â†¦ zero_le_one
        Â· simpa only [Finset.card_range, smul_eq_mul, mul_one]
      by_cases hij : j < i
      Â· exact hle _ hij.le
      Â· rw [â† Finset.sum_range_add_sum_Ico _ (not_lt.1 hij)]
        suffices (âˆ‘ k in Finset.Ico i j, (s (k + 1)).indicator 1 Ï‰) = 0 by
          rw [this, add_zero]
          exact hle _ le_rfl
        refine' Finset.sum_eq_zero fun m hm â†¦ _
        exact Set.indicator_of_not_mem (hcon _ <| (Finset.mem_Ico.1 hm).1.trans m.le_succ) _
    exact not_le.2 (lt_of_lt_of_le i.lt_succ_self <| h _ le_rfl) this
#align limsup_eq_tendsto_sum_indicator_nat_at_top limsup_eq_tendsto_sum_indicator_nat_atTop

theorem limsup_eq_tendsto_sum_indicator_atTop (R : Type*) [StrictOrderedSemiring R] [Archimedean R]
    (s : â„• â†’ Set Î±) : limsup s atTop = { Ï‰ | Tendsto
      (fun n â†¦ âˆ‘ k in Finset.range n, (s (k + 1)).indicator (1 : Î± â†’ R) Ï‰) atTop atTop } := by
  rw [limsup_eq_tendsto_sum_indicator_nat_atTop s]
  ext Ï‰
  simp only [Set.mem_setOf_eq]
  rw [(_ : (fun n â†¦ âˆ‘ k in Finset.range n, (s (k + 1)).indicator (1 : Î± â†’ R) Ï‰) = fun n â†¦
    â†‘(âˆ‘ k in Finset.range n, (s (k + 1)).indicator (1 : Î± â†’ â„•) Ï‰))]
  Â· exact tendsto_nat_cast_atTop_iff.symm
  Â· ext n
    simp only [Set.indicator, Pi.one_apply, Finset.sum_boole, Nat.cast_id]
#align limsup_eq_tendsto_sum_indicator_at_top limsup_eq_tendsto_sum_indicator_atTop

end Indicator

section LiminfLimsupAddSub

variable {R : Type*} [ConditionallyCompleteLinearOrder R] [TopologicalSpace R] [OrderTopology R]

/-- `liminf (c + xáµ¢) = c + liminf xáµ¢`. -/
lemma limsup_const_add (F : Filter Î¹) [NeBot F] [Add R] [ContinuousAdd R]
    [CovariantClass R R (fun x y â†¦ x + y) fun x y â†¦ x â‰¤ y] (f : Î¹ â†’ R) (c : R)
    (bdd_above : F.IsBoundedUnder (Â· â‰¤ Â·) f) (bdd_below : F.IsBoundedUnder (Â· â‰¥ Â·) f) :
    Filter.limsup (fun i â†¦ c + f i) F = c + Filter.limsup f F :=
  (Monotone.map_limsSup_of_continuousAt (F := F.map f) (f := fun (x : R) â†¦ c + x)
    (fun _ _ h â†¦ add_le_add_left h c) (continuous_add_left c).continuousAt bdd_above bdd_below).symm

/-- `limsup (xáµ¢ + c) = (limsup xáµ¢) + c`. -/
lemma limsup_add_const (F : Filter Î¹) [NeBot F] [Add R] [ContinuousAdd R]
    [CovariantClass R R (Function.swap fun x y â†¦ x + y) fun x y â†¦ x â‰¤ y] (f : Î¹ â†’ R) (c : R)
    (bdd_above : F.IsBoundedUnder (Â· â‰¤ Â·) f) (bdd_below : F.IsBoundedUnder (Â· â‰¥ Â·) f) :
    Filter.limsup (fun i â†¦ f i + c) F = Filter.limsup f F + c :=
  (Monotone.map_limsSup_of_continuousAt (F := F.map f) (f := fun (x : R) â†¦ x + c)
    (fun _ _ h â†¦ add_le_add_right h c)
    (continuous_add_right c).continuousAt bdd_above bdd_below).symm

/-- `liminf (c + xáµ¢) = c + limsup xáµ¢`. -/
lemma liminf_const_add (F : Filter Î¹) [NeBot F] [Add R] [ContinuousAdd R]
    [CovariantClass R R (fun x y â†¦ x + y) fun x y â†¦ x â‰¤ y]  (f : Î¹ â†’ R) (c : R)
    (bdd_above : F.IsBoundedUnder (Â· â‰¤ Â·) f) (bdd_below : F.IsBoundedUnder (Â· â‰¥ Â·) f) :
    Filter.liminf (fun i â†¦ c + f i) F = c + Filter.liminf f F :=
  (Monotone.map_limsInf_of_continuousAt (F := F.map f) (f := fun (x : R) â†¦ c + x)
    (fun _ _ h â†¦ add_le_add_left h c) (continuous_add_left c).continuousAt bdd_above bdd_below).symm

/-- `liminf (xáµ¢ + c) = (liminf xáµ¢) + c`. -/
lemma liminf_add_const (F : Filter Î¹) [NeBot F] [Add R] [ContinuousAdd R]
    [CovariantClass R R (Function.swap fun x y â†¦ x + y) fun x y â†¦ x â‰¤ y] (f : Î¹ â†’ R) (c : R)
    (bdd_above : F.IsBoundedUnder (Â· â‰¤ Â·) f) (bdd_below : F.IsBoundedUnder (Â· â‰¥ Â·) f) :
    Filter.liminf (fun i â†¦ f i + c) F = Filter.liminf f F + c :=
  (Monotone.map_limsInf_of_continuousAt (F := F.map f) (f := fun (x : R) â†¦ x + c)
    (fun _ _ h â†¦ add_le_add_right h c)
    (continuous_add_right c).continuousAt bdd_above bdd_below).symm

/-- `limsup (c - xáµ¢) = c - liminf xáµ¢`. -/
lemma limsup_const_sub (F : Filter Î¹) [NeBot F] [AddCommSemigroup R] [Sub R] [ContinuousSub R]
    [OrderedSub R] [CovariantClass R R (fun x y â†¦ x + y) fun x y â†¦ x â‰¤ y] (f : Î¹ â†’ R) (c : R)
    (bdd_above : F.IsBoundedUnder (Â· â‰¤ Â·) f) (bdd_below : F.IsBoundedUnder (Â· â‰¥ Â·) f) :
    Filter.limsup (fun i â†¦ c - f i) F = c - Filter.liminf f F :=
  (Antitone.map_limsInf_of_continuousAt (F := F.map f) (f := fun (x : R) â†¦ c - x)
    (fun _ _ h â†¦ tsub_le_tsub_left h c)
    (continuous_sub_left c).continuousAt bdd_above bdd_below).symm

/-- `limsup (xáµ¢ - c) = (limsup xáµ¢) - c`. -/
lemma limsup_sub_const (F : Filter Î¹) [NeBot F] [AddCommSemigroup R] [Sub R] [ContinuousSub R]
    [OrderedSub R] (f : Î¹ â†’ R) (c : R)
    (bdd_above : F.IsBoundedUnder (Â· â‰¤ Â·) f) (bdd_below : F.IsBoundedUnder (Â· â‰¥ Â·) f) :
    Filter.limsup (fun i â†¦ f i - c) F = Filter.limsup f F - c :=
  (Monotone.map_limsSup_of_continuousAt (F := F.map f) (f := fun (x : R) â†¦ x - c)
    (fun _ _ h â†¦ tsub_le_tsub_right h c)
    (continuous_sub_right c).continuousAt bdd_above bdd_below).symm

/-- `liminf (c - xáµ¢) = c - limsup xáµ¢`. -/
lemma liminf_const_sub (F : Filter Î¹) [NeBot F] [AddCommSemigroup R] [Sub R] [ContinuousSub R]
    [OrderedSub R] [CovariantClass R R (fun x y â†¦ x + y) fun x y â†¦ x â‰¤ y] (f : Î¹ â†’ R) (c : R)
    (bdd_above : F.IsBoundedUnder (Â· â‰¤ Â·) f) (bdd_below : F.IsBoundedUnder (Â· â‰¥ Â·) f) :
    Filter.liminf (fun i â†¦ c - f i) F = c - Filter.limsup f F :=
  (Antitone.map_limsSup_of_continuousAt (F := F.map f) (f := fun (x : R) â†¦ c - x)
    (fun _ _ h â†¦ tsub_le_tsub_left h c)
    (continuous_sub_left c).continuousAt bdd_above bdd_below).symm

/-- `liminf (xáµ¢ - c) = (liminf xáµ¢) - c`. -/
lemma liminf_sub_const (F : Filter Î¹) [NeBot F] [AddCommSemigroup R] [Sub R] [ContinuousSub R]
    [OrderedSub R] (f : Î¹ â†’ R) (c : R)
    (bdd_above : F.IsBoundedUnder (Â· â‰¤ Â·) f) (bdd_below : F.IsBoundedUnder (Â· â‰¥ Â·) f) :
    Filter.liminf (fun i â†¦ f i - c) F = Filter.liminf f F - c :=
  (Monotone.map_limsInf_of_continuousAt (F := F.map f) (f := fun (x : R) â†¦ x - c)
    (fun _ _ h â†¦ tsub_le_tsub_right h c)
    (continuous_sub_right c).continuousAt bdd_above bdd_below).symm

end LiminfLimsupAddSub -- section
