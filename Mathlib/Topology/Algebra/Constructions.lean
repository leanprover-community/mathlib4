/-
Copyright (c) 2021 Nicol√≤ Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Nicol√≤ Cavalleri
-/
import Mathlib.Topology.Separation.Hausdorff
import Mathlib.Topology.Homeomorph.Lemmas

/-!
# Topological space structure on the opposite monoid and on the units group

In this file we define `TopologicalSpace` structure on `M·µê·µí·µñ`, `M·µÉ·µí·µñ`, `MÀ£`, and `AddUnits M`.
This file does not import definitions of a topological monoid and/or a continuous multiplicative
action, so we postpone the proofs of `HasContinuousMul M·µê·µí·µñ` etc. till we have these definitions.

## Tags

topological space, opposite monoid, units
-/


variable {M N X : Type*}

open Filter Topology

namespace MulOpposite

/-- Put the same topological space structure on the opposite monoid as on the original space. -/
@[to_additive /-- Put the same topological space structure on the opposite monoid as on the original
space. -/]
instance instTopologicalSpaceMulOpposite [TopologicalSpace M] : TopologicalSpace M·µê·µí·µñ :=
  TopologicalSpace.induced (unop : M·µê·µí·µñ ‚Üí M) ‚Äπ_‚Ä∫

variable [TopologicalSpace M]

@[to_additive (attr := continuity, fun_prop)]
theorem continuous_unop : Continuous (unop : M·µê·µí·µñ ‚Üí M) :=
  continuous_induced_dom

@[to_additive (attr := continuity, fun_prop)]
theorem continuous_op : Continuous (op : M ‚Üí M·µê·µí·µñ) :=
  continuous_induced_rng.2 continuous_id

/-- `MulOpposite.op` as a homeomorphism. -/
@[to_additive (attr := simps!) /-- `AddOpposite.op` as a homeomorphism. -/]
def opHomeomorph : M ‚âÉ‚Çú M·µê·µí·µñ where
  toEquiv := opEquiv
  continuous_toFun := continuous_op
  continuous_invFun := continuous_unop

@[to_additive]
instance instT2Space [T2Space M] : T2Space M·µê·µí·µñ := opHomeomorph.t2Space

@[to_additive]
instance instDiscreteTopology [DiscreteTopology M] : DiscreteTopology M·µê·µí·µñ :=
  opHomeomorph.symm.isEmbedding.discreteTopology

@[to_additive]
instance instCompactSpace [CompactSpace M] : CompactSpace M·µê·µí·µñ :=
  opHomeomorph.compactSpace

@[to_additive]
instance instWeaklyLocallyCompactSpace [WeaklyLocallyCompactSpace M] :
    WeaklyLocallyCompactSpace M·µê·µí·µñ :=
  opHomeomorph.symm.isClosedEmbedding.weaklyLocallyCompactSpace

@[to_additive]
instance instLocallyCompactSpace [LocallyCompactSpace M] :
    LocallyCompactSpace M·µê·µí·µñ :=
  opHomeomorph.symm.isClosedEmbedding.locallyCompactSpace

@[to_additive (attr := simp)]
theorem map_op_nhds (x : M) : map (op : M ‚Üí M·µê·µí·µñ) (ùìù x) = ùìù (op x) :=
  opHomeomorph.map_nhds_eq x

@[to_additive (attr := simp)]
theorem map_unop_nhds (x : M·µê·µí·µñ) : map (unop : M·µê·µí·µñ ‚Üí M) (ùìù x) = ùìù (unop x) :=
  opHomeomorph.symm.map_nhds_eq x

@[to_additive (attr := simp)]
theorem comap_op_nhds (x : M·µê·µí·µñ) : comap (op : M ‚Üí M·µê·µí·µñ) (ùìù x) = ùìù (unop x) :=
  opHomeomorph.comap_nhds_eq x

@[to_additive (attr := simp)]
theorem comap_unop_nhds (x : M) : comap (unop : M·µê·µí·µñ ‚Üí M) (ùìù x) = ùìù (op x) :=
  opHomeomorph.symm.comap_nhds_eq x

end MulOpposite

namespace Units

open MulOpposite

variable [TopologicalSpace M] [Monoid M] [TopologicalSpace N] [Monoid N] [TopologicalSpace X]

/-- The units of a monoid are equipped with a topology, via the embedding into `M √ó M`. -/
@[to_additive
/-- The additive units of a monoid are equipped with a topology, via the embedding into `M √ó M`. -/]
instance instTopologicalSpaceUnits : TopologicalSpace MÀ£ :=
  TopologicalSpace.induced (embedProduct M) inferInstance

@[to_additive]
theorem isInducing_embedProduct : IsInducing (embedProduct M) := ‚ü®rfl‚ü©

@[to_additive]
theorem isEmbedding_embedProduct : IsEmbedding (embedProduct M) :=
  ‚ü®isInducing_embedProduct, embedProduct_injective M‚ü©

@[to_additive]
instance instT2Space [T2Space M] : T2Space MÀ£ := isEmbedding_embedProduct.t2Space

@[to_additive]
instance instDiscreteTopology [DiscreteTopology M] : DiscreteTopology MÀ£ :=
  isEmbedding_embedProduct.discreteTopology

@[to_additive] lemma topology_eq_inf :
    instTopologicalSpaceUnits =
      .induced (val : MÀ£ ‚Üí M) ‚Äπ_‚Ä∫ ‚äì .induced (fun u ‚Ü¶ ‚Üëu‚Åª¬π : MÀ£ ‚Üí M) ‚Äπ_‚Ä∫ := by
  simp only [isInducing_embedProduct.1, instTopologicalSpaceProd, induced_inf,
    instTopologicalSpaceMulOpposite, induced_compose]; rfl

/-- An auxiliary lemma that can be used to prove that coercion `MÀ£ ‚Üí M` is a topological embedding.
Use `Units.isEmbedding_val‚ÇÄ`, `Units.isEmbedding_val`, or `toUnits_homeomorph` instead. -/
@[to_additive /-- An auxiliary lemma that can be used to prove that coercion `AddUnits M ‚Üí M` is a
topological embedding. Use `AddUnits.isEmbedding_val` or `toAddUnits_homeomorph` instead. -/]
lemma isEmbedding_val_mk' {M : Type*} [Monoid M] [TopologicalSpace M] {f : M ‚Üí M}
    (hc : ContinuousOn f {x : M | IsUnit x}) (hf : ‚àÄ u : MÀ£, f u.1 = ‚Üëu‚Åª¬π) :
    IsEmbedding (val : MÀ£ ‚Üí M) := by
  refine ‚ü®‚ü®?_‚ü©, val_injective‚ü©
  rw [topology_eq_inf, inf_eq_left, ‚Üê continuous_iff_le_induced,
    @continuous_iff_continuousAt _ _ (.induced _ _)]
  intro u s hs
  simp only [‚Üê hf, nhds_induced, Filter.mem_map] at hs ‚ä¢
  exact ‚ü®_, mem_inf_principal.1 (hc u u.isUnit hs), fun u' hu' ‚Ü¶ hu' u'.isUnit‚ü©

/-- An auxiliary lemma that can be used to prove that coercion `MÀ£ ‚Üí M` is a topological embedding.
Use `Units.isEmbedding_val‚ÇÄ`, `Units.isEmbedding_val`, or `toUnits_homeomorph` instead. -/
@[to_additive /-- An auxiliary lemma that can be used to prove that coercion `AddUnits M ‚Üí M` is a
topological embedding. Use `AddUnits.isEmbedding_val` or `toAddUnits_homeomorph` instead. -/]
lemma embedding_val_mk {M : Type*} [DivisionMonoid M] [TopologicalSpace M]
    (h : ContinuousOn Inv.inv {x : M | IsUnit x}) : IsEmbedding (val : MÀ£ ‚Üí M) :=
  isEmbedding_val_mk' h fun u ‚Ü¶ (val_inv_eq_inv_val u).symm

@[to_additive]
theorem continuous_embedProduct : Continuous (embedProduct M) :=
  continuous_induced_dom

@[to_additive (attr := fun_prop)]
theorem continuous_val : Continuous ((‚Üë) : MÀ£ ‚Üí M) :=
  (@continuous_embedProduct M _ _).fst

@[to_additive]
protected theorem continuous_iff {f : X ‚Üí MÀ£} :
    Continuous f ‚Üî Continuous (val ‚àò f) ‚àß Continuous (fun x => ‚Üë(f x)‚Åª¬π : X ‚Üí M) := by
  simp only [isInducing_embedProduct.continuous_iff, embedProduct_apply, Function.comp_def,
    continuous_prodMk, opHomeomorph.symm.isInducing.continuous_iff, opHomeomorph_symm_apply,
    unop_op]

@[to_additive (attr := fun_prop)]
theorem continuous_coe_inv : Continuous (fun u => ‚Üëu‚Åª¬π : MÀ£ ‚Üí M) :=
  (Units.continuous_iff.1 continuous_id).2

@[to_additive]
lemma continuous_map {f : M ‚Üí* N} (hf : Continuous f) : Continuous (map f) :=
  Units.continuous_iff.mpr ‚ü®hf.comp continuous_val, hf.comp continuous_coe_inv‚ü©

@[to_additive]
lemma isOpenMap_map {f : M ‚Üí* N} (hf_inj : Function.Injective f) (hf : IsOpenMap f) :
    IsOpenMap (map f) := by
  rintro _ ‚ü®U, hU, rfl‚ü©
  let g : M √ó M·µê·µí·µñ ‚Üí N √ó N·µê·µí·µñ := Prod.map f (opHomeomorph ‚àò f ‚àò opHomeomorph.symm)
  have hg_openMap := hf.prodMap <| opHomeomorph.isOpenMap.comp (hf.comp opHomeomorph.symm.isOpenMap)
  refine ‚ü®g '' U, hg_openMap U hU, Set.ext fun y ‚Ü¶ ?_‚ü©
  simp only [embedProduct, OneHom.coe_mk, Set.mem_preimage, Set.mem_image, Prod.mk.injEq,
    Prod.map, Prod.exists, MulOpposite.exists, MonoidHom.coe_mk, g]
  refine ‚ü®fun ‚ü®a, b, h, ha, hb‚ü© ‚Ü¶ ‚ü®‚ü®a, b, hf_inj ?_, hf_inj ?_‚ü©, ?_‚ü©,
          fun ‚ü®x, hxV, hx‚ü© ‚Ü¶ ‚ü®x, x.inv, by simp [hxV, ‚Üê hx]‚ü©‚ü© <;>
    simp_all

end Units
