/-
Copyright (c) 2021 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri
-/
import Mathlib.Topology.Separation.Hausdorff

/-!
# Topological space structure on the opposite monoid and on the units group

In this file we define `TopologicalSpace` structure on `Máµáµ’áµ–`, `Máµƒáµ’áµ–`, `MË£`, and `AddUnits M`.
This file does not import definitions of a topological monoid and/or a continuous multiplicative
action, so we postpone the proofs of `HasContinuousMul Máµáµ’áµ–` etc till we have these definitions.

## Tags

topological space, opposite monoid, units
-/


variable {M X : Type*}

open Filter Topology

namespace MulOpposite

/-- Put the same topological space structure on the opposite monoid as on the original space. -/
@[to_additive "Put the same topological space structure on the opposite monoid as on the original
space."]
instance instTopologicalSpaceMulOpposite [TopologicalSpace M] : TopologicalSpace Máµáµ’áµ– :=
  TopologicalSpace.induced (unop : Máµáµ’áµ– â†’ M) â€¹_â€º

variable [TopologicalSpace M]

@[to_additive (attr := continuity)]
theorem continuous_unop : Continuous (unop : Máµáµ’áµ– â†’ M) :=
  continuous_induced_dom

@[to_additive (attr := continuity)]
theorem continuous_op : Continuous (op : M â†’ Máµáµ’áµ–) :=
  continuous_induced_rng.2 continuous_id

/-- `MulOpposite.op` as a homeomorphism. -/
@[to_additive (attr := simps!) "`AddOpposite.op` as a homeomorphism."]
def opHomeomorph : M â‰ƒâ‚œ Máµáµ’áµ– where
  toEquiv := opEquiv
  continuous_toFun := continuous_op
  continuous_invFun := continuous_unop

@[to_additive]
instance instT2Space [T2Space M] : T2Space Máµáµ’áµ– := opHomeomorph.t2Space

@[to_additive]
instance instDiscreteTopology [DiscreteTopology M] : DiscreteTopology Máµáµ’áµ– :=
  opHomeomorph.symm.isEmbedding.discreteTopology

@[to_additive (attr := simp)]
theorem map_op_nhds (x : M) : map (op : M â†’ Máµáµ’áµ–) (ğ“ x) = ğ“ (op x) :=
  opHomeomorph.map_nhds_eq x

@[to_additive (attr := simp)]
theorem map_unop_nhds (x : Máµáµ’áµ–) : map (unop : Máµáµ’áµ– â†’ M) (ğ“ x) = ğ“ (unop x) :=
  opHomeomorph.symm.map_nhds_eq x

@[to_additive (attr := simp)]
theorem comap_op_nhds (x : Máµáµ’áµ–) : comap (op : M â†’ Máµáµ’áµ–) (ğ“ x) = ğ“ (unop x) :=
  opHomeomorph.comap_nhds_eq x

@[to_additive (attr := simp)]
theorem comap_unop_nhds (x : M) : comap (unop : Máµáµ’áµ– â†’ M) (ğ“ x) = ğ“ (op x) :=
  opHomeomorph.symm.comap_nhds_eq x

end MulOpposite

namespace Units

open MulOpposite

variable [TopologicalSpace M] [Monoid M] [TopologicalSpace X]

/-- The units of a monoid are equipped with a topology, via the embedding into `M Ã— M`. -/
@[to_additive "The additive units of a monoid are equipped with a topology, via the embedding into
`M Ã— M`."]
instance instTopologicalSpaceUnits : TopologicalSpace MË£ :=
  TopologicalSpace.induced (embedProduct M) inferInstance

@[to_additive]
theorem isInducing_embedProduct : IsInducing (embedProduct M) := âŸ¨rflâŸ©

@[deprecated (since := "2024-10-28")] alias inducing_embedProduct := isInducing_embedProduct

@[to_additive]
theorem isEmbedding_embedProduct : IsEmbedding (embedProduct M) :=
  âŸ¨isInducing_embedProduct, embedProduct_injective MâŸ©

@[deprecated (since := "2024-10-26")]
alias embedding_embedProduct := isEmbedding_embedProduct

@[to_additive]
instance instT2Space [T2Space M] : T2Space MË£ := isEmbedding_embedProduct.t2Space

@[to_additive]
instance instDiscreteTopology [DiscreteTopology M] : DiscreteTopology MË£ :=
  isEmbedding_embedProduct.discreteTopology

@[to_additive] lemma topology_eq_inf :
    instTopologicalSpaceUnits =
      .induced (val : MË£ â†’ M) â€¹_â€º âŠ“ .induced (fun u â†¦ â†‘uâ»Â¹ : MË£ â†’ M) â€¹_â€º := by
  simp only [isInducing_embedProduct.1, instTopologicalSpaceProd, induced_inf,
    instTopologicalSpaceMulOpposite, induced_compose]; rfl

/-- An auxiliary lemma that can be used to prove that coercion `MË£ â†’ M` is a topological embedding.
Use `Units.isEmbedding_valâ‚€`, `Units.isEmbedding_val`, or `toUnits_homeomorph` instead. -/
@[to_additive "An auxiliary lemma that can be used to prove that coercion `AddUnits M â†’ M` is a
topological embedding. Use `AddUnits.isEmbedding_val` or `toAddUnits_homeomorph` instead."]
lemma isEmbedding_val_mk' {M : Type*} [Monoid M] [TopologicalSpace M] {f : M â†’ M}
    (hc : ContinuousOn f {x : M | IsUnit x}) (hf : âˆ€ u : MË£, f u.1 = â†‘uâ»Â¹) :
    IsEmbedding (val : MË£ â†’ M) := by
  refine âŸ¨âŸ¨?_âŸ©, extâŸ©
  rw [topology_eq_inf, inf_eq_left, â† continuous_iff_le_induced,
    @continuous_iff_continuousAt _ _ (.induced _ _)]
  intros u s hs
  simp only [â† hf, nhds_induced, Filter.mem_map] at hs âŠ¢
  exact âŸ¨_, mem_inf_principal.1 (hc u u.isUnit hs), fun u' hu' â†¦ hu' u'.isUnitâŸ©

@[deprecated (since := "2024-10-26")]
alias embedding_val_mk' := isEmbedding_val_mk'

/-- An auxiliary lemma that can be used to prove that coercion `MË£ â†’ M` is a topological embedding.
Use `Units.isEmbedding_valâ‚€`, `Units.isEmbedding_val`, or `toUnits_homeomorph` instead. -/
@[to_additive "An auxiliary lemma that can be used to prove that coercion `AddUnits M â†’ M` is a
topological embedding. Use `AddUnits.isEmbedding_val` or `toAddUnits_homeomorph` instead."]
lemma embedding_val_mk {M : Type*} [DivisionMonoid M] [TopologicalSpace M]
    (h : ContinuousOn Inv.inv {x : M | IsUnit x}) : IsEmbedding (val : MË£ â†’ M) :=
  isEmbedding_val_mk' h fun u â†¦ (val_inv_eq_inv_val u).symm

@[to_additive]
theorem continuous_embedProduct : Continuous (embedProduct M) :=
  continuous_induced_dom

@[to_additive (attr := fun_prop)]
theorem continuous_val : Continuous ((â†‘) : MË£ â†’ M) :=
  (@continuous_embedProduct M _ _).fst

@[to_additive]
protected theorem continuous_iff {f : X â†’ MË£} :
    Continuous f â†” Continuous (val âˆ˜ f) âˆ§ Continuous (fun x => â†‘(f x)â»Â¹ : X â†’ M) := by
  simp only [isInducing_embedProduct.continuous_iff, embedProduct_apply, Function.comp_def,
    continuous_prodMk, opHomeomorph.symm.isInducing.continuous_iff, opHomeomorph_symm_apply,
    unop_op]

@[to_additive (attr := fun_prop)]
theorem continuous_coe_inv : Continuous (fun u => â†‘uâ»Â¹ : MË£ â†’ M) :=
  (Units.continuous_iff.1 continuous_id).2

end Units
