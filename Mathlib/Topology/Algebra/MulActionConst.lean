/-
Copyright (c) 2025 Lara Toledano. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Lara Toledano
-/
module

public import Mathlib.Algebra.Group.Action.Defs
public import Mathlib.Algebra.Group.Action.Pointwise.Set.Basic
public import Mathlib.Topology.Algebra.Monoid.Defs
public import Mathlib.Topology.Algebra.Group.Basic

/-!
# Monoid actions continuous in the first variable

In this file we define the class `ContinuousSMulConst`. We say `ContinuousSMulConst Œì T` if
`Œì` acts on `T` and for each `x`, the map `Œ≥ ‚Ü¶ Œ≥ ‚Ä¢ x` is continuous. (This is analogous to
`ContinuousConstSMul` and differs from `ContinuousSMul`, which requires simultaneous continuity in
both variables.)

-/

@[expose] public section

section ball
variable {G X : Type*}

open scoped Pointwise

namespace MulAction

/--
The set `{g ‚Ä¢ x | g ‚àà U}`. Intended to be used for `U ‚àà ùìù 1`. Named in analogy to
`UniformSpace.ball`.
-/
abbrev ball [SMul G X] (x : X) (U : Set G) : Set X := U ‚Ä¢ {x}

lemma ball_eq_image [SMul G X] (x : X) (U : Set G) : U ‚Ä¢ {x} = (¬∑ ‚Ä¢ x)'' U := by simp

lemma mem_ball_iff [SMul G X] {x : X} {U : Set G} {y : X} :
    y ‚àà U ‚Ä¢ ({x} : Set X) ‚Üî ‚àÉ g ‚àà U, g ‚Ä¢ x = y := by simp

lemma ball_iUnion [SMul G X] (x : X) {Œπ : Type*} (U : Œπ ‚Üí Set G) :
    (‚ãÉ i, U i) ‚Ä¢ {x} = ‚ãÉ i, (U i) ‚Ä¢ {x} := by
  simp [Set.image_iUnion]

lemma ball_iUnion‚ÇÇ [SMul G X] (x : X) {Œπ : Type*} {Œ∫ : Œπ ‚Üí Sort*} (U : (i : Œπ) ‚Üí Œ∫ i ‚Üí Set G) :
    (‚ãÉ i, ‚ãÉ j, U i j) ‚Ä¢ {x} = ‚ãÉ i, ‚ãÉ j, (U i j) ‚Ä¢ {x} := by simp [Set.image_iUnion‚ÇÇ]

lemma ball_mul [Monoid G] [MulAction G X] (x : X) (U V : Set G) :
    ((U * V) ‚Ä¢ {x} : Set X) = U ‚Ä¢ (V ‚Ä¢ {x}) := by
  ext y
  simp [Set.mem_smul, mul_smul]

lemma ball_smul [Monoid G] [MulAction G X] (x : X) (g : G) (U : Set G) :
    (g ‚Ä¢ U) ‚Ä¢ ({x} : Set X) = g ‚Ä¢ (U ‚Ä¢ {x}) := by
  calc (g ‚Ä¢ U) ‚Ä¢ {x}
    _ = ({g} ‚Ä¢ U) ‚Ä¢ {x} := by rw [Set.singleton_smul]
    _ = ({g} * U) ‚Ä¢ {x} := by rw [smul_eq_mul]
    _ = {g} ‚Ä¢ (U ‚Ä¢ {x}) := by rw [ball_mul]
    _ = g ‚Ä¢ (U ‚Ä¢ {x}) := by simp

lemma ball_inter [SMul G X] (x : X) (U V : Set G) :
    (U ‚à© V) ‚Ä¢ ({x} : Set X) ‚äÜ U ‚Ä¢ {x} ‚à© V ‚Ä¢ {x} := by
  simp_rw [MulAction.ball_eq_image]
  apply Set.image_inter_subset

open scoped RightActions
lemma ball_op_smul [Monoid G] [MulAction G X] (x : X) (g : G) (U : Set G) :
    (U <‚Ä¢ g) ‚Ä¢ ({x} : Set X) = U ‚Ä¢ {g ‚Ä¢ x} := by
  calc (U <‚Ä¢ g) ‚Ä¢ ({x} : Set X)
    _ = (U * {g}) ‚Ä¢ {x} := by rw [Set.mul_singleton]; rfl
    _ = U ‚Ä¢ ({g} ‚Ä¢ {x}) := ball_mul ..
    _ = U ‚Ä¢ {g ‚Ä¢ x} := by simp
end MulAction

end ball

section ContinuousSMulConst
/-! The `ContinuousSMulConst` class. -/

/-- Class `ContinuousSMulConst Œì T` says that the scalar multiplication `(‚Ä¢) : Œì ‚Üí T ‚Üí T`
is continuous in the first argument. We use the same class for all kinds of multiplicative
actions, including (semi)modules and algebras. -/
@[mk_iff] class ContinuousSMulConst (Œì : Type*) (T : Type*)
    [TopologicalSpace Œì] [TopologicalSpace T] [SMul Œì T] : Prop where
  /-- The scalar multiplication `(‚Ä¢) : Œì ‚Üí T ‚Üí T` is continuous in the first argument. -/
  continuous_smul_const : ‚àÄ x : T, Continuous fun Œ≥ : Œì => Œ≥ ‚Ä¢ x

instance (priority := 100) ContinuousMul.toContinuousSMulConst (M : Type*) [TopologicalSpace M]
    [Mul M] [ContinuousMul M] : ContinuousSMulConst M M where
  continuous_smul_const := continuous_mul_right

lemma continuous_smul_const (Œì : Type*) {T : Type*} [TopologicalSpace Œì] [TopologicalSpace T]
    [SMul Œì T] [ContinuousSMulConst Œì T] (x : T) : Continuous fun Œ≥ : Œì => Œ≥ ‚Ä¢ x :=
  ContinuousSMulConst.continuous_smul_const x

end ContinuousSMulConst
