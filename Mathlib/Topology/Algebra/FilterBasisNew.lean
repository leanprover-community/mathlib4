/-
Copyright (c) 2021 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Anatole Dedecker
-/
import Mathlib.Order.Filter.Bases.Basic
import Mathlib.Topology.Algebra.Module.Basic

/-!
**WARNING** : This is a new version of `Topology.Algebra.FilterBasis`, which is still used
by the library at this point in time. The library will be gradually modified to use this file
instead. See `https://leanprover.zulipchat.com/#narrow/channel/287929-mathlib4/topic/Refactoring.20algebraic.20filter.20bases/near/479437345`
for more details.

# Algebraic filter bases

We know that the topology of a topological group (or ring, or module) is invariant by
translations, hence it is completely described by its filter of neighborhoods at the
neutral element (`1` for groups, `0` for additive groups, rings and modules).
In this file, we completely characterize the filter bases which can be obtained as bases of
neighborhoods of the neutral element for some group (or ring, or module) topology.

More precisely, for `X` in `{Group, AddGroup, Ring, Module}`, we provide :
* `Filter.IsXBasis p s`: a strenghtening of `Filter.IsBasis` with compatibility conditions with the
  algebraic structure.
* `Filter.IsXBasis.mk_of_comm`: simpler contructors in the commutative setting.
* `Filter.HasBasis.isXBasis`: in a topological `X`, *any* basis of neighborhoods
  of the neutral element satisfies `Filter.IsXBasis`.
* `Filter.IsXBasis.topologicalX_of_hasBasis`: if some `X` comes *equipped* with a topology and
  a basis of neighborhoods of the neutral element satisfies `Filter.IsXBasis`, then that topology is
  a `X` topology. This requires the topology to be invariant by translation.
* `Filter.IsXBasis.topology`: the topological structure associated to some `X` filter basis.
* `Filter.IsXBasis.topologicalX`: the topology associated to some `X` filter basis is indeed
  an `X` topology.

Note the subtlety between `IsXBasis.topologicalX_of_hasBasis`, useful for *proving* that some
topology is compatible with the algebraic structure, and `IsXBasis.topologicalX` which shows
that *the* topology associated to some basis is compatible with the algebraic structure.
Mathematically this is the same thing, but having these variations is convenient.

Remark: for `X = Module`, we use `continuousSMul` instead of `topologicalModule` in the names.

## Usage indications

The results in this file are meant to be used for either
**proving some compatibility between topological and algebraic structures**
or **constructing some topology compatible with an algebraic structure**.

Thus, once you have all the relevant instances on your favorite type, you probably
don't want to use this file anymore.

## Implementation Notes

For a long time, the definitions in this file were built on top of `FilterBasis`.
We switched to `Filter.IsBasis` instead, because its API is much closer to
that of `Filter.HasBasis`, which is what we use most of the time (when we already
know the generated filter).

## References

* [N. Bourbaki, *General Topology*][bourbaki1966]
-/

open Filter Set TopologicalSpace Function

open Topology Filter Pointwise

universe u

namespace Filter

/-!
## Filter bases for group topologies
-/

/-- Given a group `G`, `s : Î¹ â†’ Set G` and `p : Î¹ â†’ Prop`, `Filter.IsGroupBasis p s` is a
strengthening of `Filter.IsBasis p s` with extra compatibility axioms ensuring that the generated
filter is the filter of neighborhoods of one for some group topology on `G`
(`Filter.IsGroupBasis.topologicalGroup`). Conversely, *any* basis of neighborhoods of one
in a topological group satisfies these conditions (`Filter.HasBasis.isGroupBasis`). -/
structure IsGroupBasis {G : Type*} {Î¹ : Sort*} [Group G] (p : Î¹ â†’ Prop) (s : Î¹ â†’ Set G) : Prop
    extends IsBasis p s where
  one : âˆ€ {i}, p i â†’ (1 : G) âˆˆ s i
  mul : âˆ€ {i}, p i â†’ âˆƒ j, p j âˆ§ s j * s j âŠ† s i
  inv : âˆ€ {i}, p i â†’ âˆƒ j, p j âˆ§ s j âŠ† (s i)â»Â¹
  conj : âˆ€ xâ‚€, âˆ€ {i}, p i â†’ âˆƒ j, p j âˆ§ MapsTo (xâ‚€ * Â· * xâ‚€â»Â¹) (s j) (s i)

/-- Given an additive group `G`, `s : Î¹ â†’ Set G` and `p : Î¹ â†’ Prop`, `Filter.IsAddGroupBasis p s`
is a strengthening of `Filter.IsBasis p s` with extra compatibility axioms ensuring that the
generated filter is the filter of neighborhoods of zero for some group topology on `G`
(`Filter.IsAddGroupBasis.topologicalAddGroup`). Conversely, *any* basis of neighborhoods of
zero in an additive topological group satisfies these conditions
(`Filter.HasBasis.isAddGroupBasis`). -/
structure IsAddGroupBasis {G : Type*} {Î¹ : Sort*} [AddGroup G] (p : Î¹ â†’ Prop) (s : Î¹ â†’ Set G) : Prop
    extends IsBasis p s where
  zero : âˆ€ {i}, p i â†’ (0 : G) âˆˆ s i
  add : âˆ€ {i}, p i â†’ âˆƒ j, p j âˆ§ s j + s j âŠ† s i
  neg : âˆ€ {i}, p i â†’ âˆƒ j, p j âˆ§ s j âŠ† -(s i)
  conj : âˆ€ xâ‚€, âˆ€ {i}, p i â†’ âˆƒ j, p j âˆ§ MapsTo (xâ‚€ + Â· + -xâ‚€) (s j) (s i)

attribute [to_additive existing] IsGroupBasis

/-- A constructor for `IsGroupBasis` in the commutative case. -/
@[to_additive "A constructor for `IsAddGroupBasis` in the commutative case."]
theorem IsGroupBasis.mk_of_comm {G : Type*} {Î¹ : Sort*} [CommGroup G] (p : Î¹ â†’ Prop) (s : Î¹ â†’ Set G)
    (toIsBasis : IsBasis p s) (one : âˆ€ {i}, p i â†’ (1 : G) âˆˆ s i)
    (mul : âˆ€ {i}, p i â†’ âˆƒ j, p j âˆ§ s j * s j âŠ† s i)
    (inv : âˆ€ {i}, p i â†’ âˆƒ j, p j âˆ§ s j âŠ† (s i)â»Â¹) :
    IsGroupBasis p s where
  toIsBasis := toIsBasis
  one := one
  mul := mul
  inv := inv
  conj x i hi := âŸ¨i, hi, by simpa only [mul_inv_cancel_comm, preimage_id'] using mapsTo_id _âŸ©

/-- In a topological group, *any* basis of neighborhoods of one is a group filter
basis. -/
@[to_additive "In an additive topological group, *any* basis of neighborhoods of zero is a group
filter basis."]
theorem HasBasis.isGroupBasis {G : Type*} {Î¹ : Sort*} [Group G] [TopologicalSpace G]
    [IsTopologicalGroup G] {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set G} (h : (ð“ 1).HasBasis p s) :
    IsGroupBasis p s where
  toIsBasis := h.isBasis
  one hi := mem_of_mem_nhds (h.mem_of_mem hi)
  mul := by
    have : Tendsto (fun p : G Ã— G â†¦ p.1 * p.2) (ð“ 1 Ã—Ë¢ ð“ 1) (ð“ 1) := by
      simpa only [nhds_prod_eq, one_mul] using (tendsto_mul (M := G) (a := 1) (b := 1))
    simpa [h.prod_self.tendsto_iff h, mul_subset_iff, forall_mem_comm] using this
  inv := by
    have : Tendsto (Â·â»Â¹ : G â†’ G) (ð“ 1) (ð“ 1) := by simpa using tendsto_inv (1 : G)
    rwa [h.tendsto_iff h] at this
  conj xâ‚€ := by
    have : Tendsto (xâ‚€ * Â· * xâ‚€â»Â¹ : G â†’ G) (ð“ 1) (ð“ 1) := by simpa using
      (tendsto_id (x := ð“ 1) |>.const_mul xâ‚€ |>.mul_const xâ‚€â»Â¹)
    rwa [h.tendsto_iff h] at this

/-- The trivial group filter basis, inducing the discrete topology. -/
example {G : Type*} [Group G] :
    IsGroupBasis (fun _ â†¦ True) (fun _ â†¦ {1} : Unit â†’ Set G) :=
  letI : TopologicalSpace G := âŠ¥
  haveI : DiscreteTopology G := âŸ¨rflâŸ©
  -- TODO: this should be inferred
  haveI : IsTopologicalGroup G := âŸ¨âŸ©
  (nhds_discrete G â–¸ hasBasis_pure 1).isGroupBasis

namespace IsGroupBasis

variable {G : Type*} {Î¹ : Sort*} [Group G] {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set G} (hB : IsGroupBasis p s)
include hB

/-!
### Proving `IsTopologicalGroup` from `Filter.IsGroupBasis`
-/

/-- Assume the group `G` is given a topology which is invariant by translations,
which we express as `ContinuousConstSMul G G`.
To show that it is a group topology, it suffices so exhibit a basis of neighborhoods of
one satisfying `Filter.IsGroupBasis`. -/
@[to_additive "Assume the group `G` is given a topology which is invariant by translations,
which we express as `ContinuousConstVAdd G G`.
To show that it is a group topology, it suffices so exhibit a basis of neighborhoods of
zero satisfying `Filter.IsAddGroupBasis`."]
lemma topologicalGroup_of_hasBasis [TopologicalSpace G] [ContinuousConstSMul G G]
    (hB' : (ð“ 1).HasBasis p s) : IsTopologicalGroup G := by
  apply IsTopologicalGroup.of_nhds_one
  Â· refine hB'.prod_self.tendsto_iff hB' |>.mpr fun i hi â†¦ (hB.mul hi).imp
      fun j âŸ¨hj, hjiâŸ© â†¦ âŸ¨hj, ?_âŸ©
    simpa [â† image2_mul, forall_mem_comm] using hji
  Â· exact hB'.tendsto_iff hB' |>.mpr fun i hi â†¦ (hB.inv hi).imp fun j â†¦ id
  Â· intro xâ‚€
    simp_rw [â† smul_eq_mul, â† Homeomorph.smul_apply xâ‚€, (Homeomorph.smul xâ‚€).map_nhds_eq,
      Homeomorph.smul_apply xâ‚€, smul_eq_mul, mul_one]
  Â· exact fun xâ‚€ â†¦ hB'.tendsto_iff hB' |>.mpr fun i hi â†¦ (hB.conj xâ‚€ hi).imp fun j â†¦ id

/-!
### Constructing a group topology from `Filter.IsGroupBasis`
-/

/-- The topological space structure coming from a group filter basis. -/
@[to_additive "The topological space structure coming from an additive group filter basis."]
def topology : TopologicalSpace G :=
  TopologicalSpace.mkOfNhds (Â· â€¢ hB.filter)

@[to_additive]
theorem nhds_eq {xâ‚€ : G} : @nhds G hB.topology xâ‚€ = xâ‚€ â€¢ hB.filter := by
  apply TopologicalSpace.nhds_mkOfNhds_of_hasBasis (fun x â†¦ hB.hasBasis.map (x * Â·))
  Â· intro a i hi
    exact âŸ¨1, hB.one hi, mul_one aâŸ©
  Â· intro a i hi
    rcases hB.mul hi with âŸ¨j, hj, hjiâŸ©
    filter_upwards [hB.hasBasis.map _ |>.mem_of_mem hj]
    rintro _ âŸ¨x, hx, rflâŸ©
    calc
      (a * x) â€¢ (s j) âˆˆ (a * x) â€¢ hB.filter := smul_set_mem_smul_filter (hB.hasBasis.mem_of_mem hj)
      _ = a â€¢ x â€¢ (s j) := smul_smul .. |>.symm
      _ âŠ† a â€¢ (s j * s j) := smul_set_mono <| smul_set_subset_smul hx
      _ âŠ† a â€¢ (s i) := smul_set_mono hji

@[to_additive]
theorem nhds_one_eq :
    @nhds G hB.topology (1 : G) = hB.filter := by
  rw [hB.nhds_eq, one_smul]

@[to_additive]
theorem nhds_hasBasis (xâ‚€ : G) :
    HasBasis (@nhds G hB.topology xâ‚€) p (fun i â†¦ xâ‚€ â€¢ (s i)) := by
  rw [hB.nhds_eq]
  apply hB.hasBasis.map _

@[to_additive]
theorem nhds_one_hasBasis :
    HasBasis (@nhds G hB.topology 1) p s := by
  rw [hB.nhds_one_eq]
  exact hB.hasBasis

@[to_additive]
theorem mem_nhds_one {i} (hi : p i) :
    s i âˆˆ @nhds G hB.topology 1 :=
  hB.nhds_one_hasBasis.mem_of_mem hi

-- See note [lower instance priority]
/-- If a group is endowed with the topological structure coming from a group filter basis, then
(by definition) this topology is invariant by translations. -/
@[to_additive "If a group is endowed with the topological structure coming from a group filter
basis, then (by definition) this topology is invariant by translations."]
instance (priority := 100) continuousConstSMul :
    @ContinuousConstSMul G G hB.topology _ := by
  letI := hB.topology
  refine âŸ¨?_âŸ©
  simp_rw [continuous_iff_continuousAt, ContinuousAt, Tendsto, nhds_eq, â† Filter.map_smul,
    smul_eq_mul, map_map, comp_mul_left, le_refl, implies_true]

-- See note [lower instance priority]
/-- If a group is endowed with the topological structure coming from a group filter basis, then
it's a topological group. -/
@[to_additive "If a group is endowed with the topological structure coming from a group filter basis
then it's a topological group."]
instance (priority := 100) topologicalGroup :
    @IsTopologicalGroup G hB.topology _ := by
  letI := hB.topology
  exact hB.topologicalGroup_of_hasBasis hB.nhds_one_hasBasis

end IsGroupBasis

/-!
## Filter bases for ring topologies
-/

/-- Given a ring `R`, `s : Î¹ â†’ Set R` and `p : Î¹ â†’ Prop`, `Filter.IsRingBasis p s` is a
strengthening of `Filter.IsBasis p s` with extra compatibility axioms ensuring that the generated
filter is the filter of neighborhoods of zero for some ring topology on `R`
(`Filter.IsRingBasis.topologicalRing`). Conversely, *any* basis of neighborhoods of zero in a
topological ring satisfies these conditions (`Filter.HasBasis.isRingBasis`). -/
structure IsRingBasis {R : Type*} {Î¹ : Sort*} [Ring R] (p : Î¹ â†’ Prop) (s : Î¹ â†’ Set R) : Prop
    extends IsAddGroupBasis p s where
  mul : âˆ€ {i}, p i â†’ âˆƒ j, p j âˆ§ s j * s j âŠ† s i
  mul_left : âˆ€ (xâ‚€ : R) {i}, p i â†’ âˆƒ j, p j âˆ§ MapsTo (xâ‚€ * Â·) (s j) (s i)
  mul_right : âˆ€ (xâ‚€ : R) {i}, p i â†’ âˆƒ j, p j âˆ§ MapsTo (Â· * xâ‚€) (s j) (s i)

/-- A constructor for `IsRingBasis` in the commutative case. -/
theorem IsRingBasis.mk_of_comm {R : Type*} {Î¹ : Sort*} [CommRing R] (p : Î¹ â†’ Prop) (s : Î¹ â†’ Set R)
    (toIsAddGroupBasis : IsAddGroupBasis p s) (mul : âˆ€ {i}, p i â†’ âˆƒ j, p j âˆ§ s j * s j âŠ† s i)
    (mul_left : âˆ€ (xâ‚€ : R) {i}, p i â†’ âˆƒ j, p j âˆ§ MapsTo (xâ‚€ * Â·) (s j) (s i)) :
    IsRingBasis p s where
  toIsAddGroupBasis := toIsAddGroupBasis
  mul := mul
  mul_left := mul_left
  mul_right := by simpa only [mul_comm] using mul_left

/-- In a topological ring, *any* basis of neighborhoods of zero is a ring filter
basis. -/
theorem HasBasis.isRingBasis {R : Type*} {Î¹ : Sort*} [Ring R] [TopologicalSpace R]
    [IsTopologicalRing R] {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set R} (h : (ð“ 0).HasBasis p s) :
    IsRingBasis p s where
  toIsAddGroupBasis := h.isAddGroupBasis
  mul := by
    have : Tendsto (fun p : R Ã— R â†¦ p.1 * p.2) (ð“ 0 Ã—Ë¢ ð“ 0) (ð“ 0) := by
      simpa only [nhds_prod_eq, zero_mul] using (tendsto_mul (M := R) (a := 0) (b := 0))
    simpa [h.prod_self.tendsto_iff h, mul_subset_iff, forall_mem_comm] using this
  mul_left xâ‚€ := by
    have : Tendsto (xâ‚€ * Â·) (ð“ 0) (ð“ 0) := by simpa using (tendsto_id (x := ð“ 0) |>.const_mul xâ‚€)
    rwa [h.tendsto_iff h] at this
  mul_right xâ‚€ := by
    have : Tendsto (Â· * xâ‚€) (ð“ 0) (ð“ 0) := by simpa using (tendsto_id (x := ð“ 0) |>.mul_const xâ‚€)
    rwa [h.tendsto_iff h] at this

/-- The trivial ring filter basis, inducing the discrete topology. -/
example {R : Type*} [Ring R] :
    IsRingBasis (fun _ â†¦ True) (fun _ â†¦ {0} : Unit â†’ Set R) :=
  letI : TopologicalSpace R := âŠ¥
  haveI : DiscreteTopology R := âŸ¨rflâŸ©
  (nhds_discrete R â–¸ hasBasis_pure 0).isRingBasis

namespace IsRingBasis

variable {R : Type*} {Î¹ : Sort*} [Ring R] {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set R} (hB : IsRingBasis p s)
include hB

/-!
### Proving `IsTopologicalRing` from `Filter.IsRingBasis`
-/

/-- Assume the ring `R` is given a topology which is invariant by translations,
which we express as `ContinuousConstVAdd R R`.
To show that it is a ring topology, it suffices so exhibit a basis of neighborhoods of
zero satisfying `Filter.IsRingBasis`. -/
lemma topologicalRing_of_hasBasis [TopologicalSpace R] [ContinuousConstVAdd R R]
    (hB' : (ð“ 0).HasBasis p s) : IsTopologicalRing R := by
  haveI := hB.topologicalAddGroup_of_hasBasis hB'
  apply IsTopologicalRing.of_addGroup_of_nhds_zero
  Â· refine hB'.prod_self.tendsto_iff hB' |>.mpr fun i hi â†¦ (hB.mul hi).imp
      fun j âŸ¨hj, hjiâŸ© â†¦ âŸ¨hj, ?_âŸ©
    simpa [â† image2_mul, forall_mem_comm] using hji
  Â· exact fun xâ‚€ â†¦ hB'.tendsto_iff hB' |>.mpr fun i hi â†¦ (hB.mul_left xâ‚€ hi).imp fun j â†¦ id
  Â· exact fun xâ‚€ â†¦ hB'.tendsto_iff hB' |>.mpr fun i hi â†¦ (hB.mul_right xâ‚€ hi).imp fun j â†¦ id

/-!
### Constructing a ring topology from `Filter.IsRingBasis`
-/

/-- The topological space structure coming from a ring filter basis.
It has the given basis as a basis of neighborhoods of zero. -/
nonrec abbrev topology : TopologicalSpace R := hB.topology

/-- If a ring is endowed with the topological structure coming from
a ring filter basis, then it's a topological ring. -/
instance (priority := 100) topologicalRing :
    @IsTopologicalRing R hB.topology _ := by
  letI := hB.topology
  haveI := hB.continuousConstVAdd
  exact hB.topologicalRing_of_hasBasis hB.nhds_zero_hasBasis

end IsRingBasis

/-!
## Filter bases for module topologies
-/

/-- Consider a topological ring `R`, a `R`-module `M`, `s : Î¹ â†’ Set M` and `p : Î¹ â†’ Prop`.
`Filter.IsModuleBasis R p s` is a strengthening of `Filter.IsBasis p s` with extra compatibility
axioms ensuring that the generated filter is the filter of neighborhoods of zero for some
`R`-module topology on `R` (`Filter.IsModuleBasis.continuousSMul`). Conversely, *any* basis of
neighborhoods of zero in a topological `R`-module satisfies these conditions
(`Filter.HasBasis.isModuleBasis`). -/
structure IsModuleBasis (R : Type*) {M : Type*} {Î¹ : Sort*} [Ring R] [TopologicalSpace R]
    [AddCommGroup M] [Module R M] (p : Î¹ â†’ Prop) (s : Î¹ â†’ Set M) : Prop
    extends IsAddGroupBasis p s where
  smul : âˆ€ {i}, p i â†’ âˆƒ V âˆˆ ð“ (0 : R), âˆƒ j, p j âˆ§ V â€¢ (s j) âŠ† s i
  smul_left : âˆ€ (xâ‚€ : R) {i}, p i â†’ âˆƒ j, p j âˆ§ MapsTo (xâ‚€ â€¢ Â·) (s j) (s i)
  smul_right : âˆ€ (mâ‚€ : M) {i}, p i â†’ âˆ€á¶  x in ð“ (0 : R), x â€¢ mâ‚€ âˆˆ s i

/-- A constructor for `IsModuleBasis` if we have a preferred basis of neighborhoods of zero
in the base ring. In particular, this applies when the ring topology comes from
`Filter.IsRingBasis`. -/
theorem IsModuleBasis.mk_of_hasBasis {R M : Type*} {Î¹R Î¹M : Sort*} [Ring R] [TopologicalSpace R]
    [AddCommGroup M] [Module R M] {pR : Î¹R â†’ Prop} {sR : Î¹R â†’ Set R} (hR : (ð“ 0).HasBasis pR sR)
    (pM : Î¹M â†’ Prop) (sM : Î¹M â†’ Set M) (toIsAddGroupBasis : IsAddGroupBasis pM sM)
    (smul : âˆ€ {i}, pM i â†’ âˆƒ j, pR j âˆ§ âˆƒ k, pM k âˆ§ (sR j) â€¢ (sM k) âŠ† sM i)
    (smul_left : âˆ€ (xâ‚€ : R) {i}, pM i â†’ âˆƒ j, pM j âˆ§ MapsTo (xâ‚€ â€¢ Â·) (sM j) (sM i))
    (smul_right : âˆ€ (mâ‚€ : M) {i}, pM i â†’ âˆƒ j, pR j âˆ§ MapsTo (Â· â€¢ mâ‚€) (sR j) (sM i)) :
    IsModuleBasis R pM sM where
  toIsAddGroupBasis := toIsAddGroupBasis
  smul hi := smul hi |>.imp' sR fun _ â†¦ And.imp_left <| hR.mem_of_mem
  smul_left := smul_left
  smul_right mâ‚€ _ hi := hR.eventually_iff.mpr <| smul_right mâ‚€ hi

/-- In a topological `R`-module, *any* basis of neighborhoods of zero is a module filter basis. -/
theorem HasBasis.isModuleBasis (R : Type*) {M : Type*} {Î¹ : Sort*} [Ring R] [TopologicalSpace R]
    [AddCommGroup M] [Module R M] [TopologicalSpace M] [IsTopologicalAddGroup M]
    [ContinuousSMul R M]
    {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set M} (h : (ð“ 0).HasBasis p s) :
    IsModuleBasis R p s where
  toIsAddGroupBasis := h.isAddGroupBasis
  smul := by
    have : Tendsto (fun p â†¦ p.1 â€¢ p.2 : R Ã— M â†’ M) (ð“ 0 Ã—Ë¢ ð“ 0) (ð“ 0 : Filter M) := by
      simpa only [nhds_prod_eq, smul_zero] using
        (tendsto_fst (Î± := R) (Î² := M) (f := ð“ 0) (g := ð“ 0)).smul tendsto_snd
    simpa [(basis_sets _ |>.prod_pprod h).tendsto_iff h, and_assoc, exists_and_left,
      smul_subset_iff, forall_comm (Î± := _ âˆˆ _) (Î² := M)] using this
  smul_left xâ‚€ := by
    have : Tendsto (xâ‚€ â€¢ Â· : M â†’ M) (ð“ 0) (ð“ 0) := by
      simpa [smul_zero] using (tendsto_id (Î± := M) (x := ð“ 0) |>.const_smul xâ‚€)
    rwa [h.tendsto_iff h] at this
  smul_right mâ‚€ := by
    have : Tendsto (Â· â€¢ mâ‚€ : R â†’ M) (ð“ 0) (ð“ 0) := by
      simpa using (tendsto_id (Î± := R) (x := ð“ 0) |>.smul_const mâ‚€)
    rwa [h.tendsto_right_iff] at this

namespace IsModuleBasis

variable {R M : Type*} {Î¹ : Sort*} [Ring R] [TopologicalSpace R]
    [AddCommGroup M] [Module R M] {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set M} (hB : IsModuleBasis R p s)
include hB

/-- If `R` is discrete then the trivial additive group filter basis on any `R`-module is a
module filter basis. -/
example [DiscreteTopology R] : IsModuleBasis R (fun _ â†¦ True) (fun _ â†¦ {0} : Unit â†’ Set M) :=
  letI : TopologicalSpace M := âŠ¥
  haveI : DiscreteTopology M := âŸ¨rflâŸ©
  -- TODO: these should be inferred
  haveI : IsTopologicalAddGroup M := âŸ¨âŸ©
  haveI : ContinuousSMul R M := âŸ¨continuous_of_discreteTopologyâŸ©
  (nhds_discrete M â–¸ hasBasis_pure 0).isModuleBasis R

/-!
### Proving `ContinuousSMul` from `Filter.IsModuleBasis`
-/

/-- Assume the `R`-module `M` is given a topology which is invariant by translations,
which we express as `ContinuousConstVAdd M M`.
To show that it is a `R`-module topology, it suffices so exhibit a basis of neighborhoods of
zero satisfying `Filter.IsModuleBasis R`. -/
theorem continuousSMul_of_hasBasis [IsTopologicalRing R] [TopologicalSpace M]
    [ContinuousConstVAdd M M] (hB' : (ð“ 0).HasBasis p s) : ContinuousSMul R M := by
  haveI := hB.topologicalAddGroup_of_hasBasis hB'
  apply ContinuousSMul.of_nhds_zero
  Â· refine basis_sets _ |>.prod_pprod hB' |>.tendsto_iff hB' |>.mpr fun i hi â†¦
      let âŸ¨V, hV, j, hj, hVjâŸ© := (hB.smul hi); âŸ¨âŸ¨V, jâŸ©, âŸ¨hV, hjâŸ©, ?_âŸ©
    simpa [forall_swap (Î± := M), â† image2_smul] using hVj
  Â· exact fun mâ‚€ â†¦ hB'.tendsto_right_iff.mpr fun i hi â†¦ hB.smul_right mâ‚€ hi
  Â· exact fun xâ‚€ â†¦ hB'.tendsto_iff hB' |>.mpr fun i hi â†¦ (hB.smul_left xâ‚€ hi).imp fun j â†¦ id

/-!
### Constructing a module topology from `Filter.IsModuleBasis`
-/

/-- The topology associated to a module filter basis on a module over a topological ring.
It has the given basis as a basis of neighborhoods of zero. -/
nonrec abbrev topology : TopologicalSpace M := hB.topology

/-- The topology associated to a module filter basis on a module over a topological ring.
It has the given basis as a basis of neighborhoods of zero. This version gets the ring
topology by unification instead of type class inference. -/
abbrev topology' {R M : Type*} {Î¹ : Sort*} [CommRing R] {_ : TopologicalSpace R}
    [AddCommGroup M] [Module R M] {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set M} (hB : IsModuleBasis R p s) :
    TopologicalSpace M :=
  hB.topology

/-- If a module is endowed with the topological structure coming from
a module filter basis then it's a topological module. -/
instance (priority := 100) continuousSMul [IsTopologicalRing R] :
    @ContinuousSMul R M _ _ hB.topology := by
  letI := hB.topology
  haveI := hB.continuousConstVAdd
  exact hB.continuousSMul_of_hasBasis hB.nhds_zero_hasBasis

end IsModuleBasis

end Filter
