/-
Copyright (c) 2025 Attila G√°sp√°r. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Attila G√°sp√°r
-/
import Mathlib.Analysis.Convex.Between
import Mathlib.Analysis.Convex.Topology
import Mathlib.Topology.Algebra.Group.AddTorsor

/-!
# Asymptotic cone of a set

This file defines the asymptotic cone of a set in a topological affine space.

## Implementation details

The asymptotic cone of a set $A$ is usually defined as the set of points $v$ for which there exist
sequences $t_n > 0$ and $x_n \in A$ such that $t_n \to 0$ and $t_n x_n \to v$. We take a different
approach here using filters: we define the asymptotic cone of `s` as the set of vectors `v` such
that `‚àÉ·∂† p in Filter.atTop ‚Ä¢ ùìù v, p ‚àà s` holds.

## Main definitions

* `AffineSpace.asymptoticNhds`: the filter of neighborhoods at infinity in some direction.
* `asymptoticCone`: the asymptotic cone of a subset of a topological affine space.

## Main statements

* `Convex.smul_vadd_mem_of_isClosed_of_mem_asymptoticCone`: if `v` is in the asymptotic cone of a
closed convex set `s`, then every ray of direction `v` starting from `s` is contained in `s`.
* `Convex.smul_vadd_mem_of_mem_nhds_of_mem_asymptoticCone`: if `v` is in the asymptotic cone of a
convex set `s`, then every ray of direction `v` starting from the interior of `s` is contained in
`s`.
-/

open scoped Pointwise Topology
open Filter

section General

variable
  {k V P : Type*}
  [Field k] [LinearOrder k] [AddCommGroup V] [Module k V] [AddTorsor V P] [TopologicalSpace V]

namespace AffineSpace

variable (k P) in
/-- In a topological affine space `P` over `k`, `AffineSpace.asymptoticNhds k P v` is the filter of
neighborhoods at infinity in directions near `v`. In a topological vector space, this is the filter
`Filter.atTop ‚Ä¢ ùìù v`. To support affine spaces, the actual definition is different and should be
considered an implementation detail. Use `AffineSpace.asymptoticNhds_eq_smul` or
`AffineSpace.asymptoticNhds_eq_smul_vadd` for unfolding. -/
@[irreducible]
def asymptoticNhds (v : V) : Filter P := ‚®Ü p, atTop (Œ± := k) ‚Ä¢ ùìù v +·µ• pure p

theorem asymptoticNhds_vadd_pure (v : V) (p : P) :
    asymptoticNhds k V v +·µ• pure p = asymptoticNhds k P v := by
  simp_rw [asymptoticNhds, vadd_pure, map_iSup, map_map, Function.comp_def]
  refine (Equiv.vaddConst p).iSup_congr fun _ => ?_
  simp [add_vadd]

theorem vadd_asymptoticNhds (u v : V) : u +·µ• asymptoticNhds k P v = asymptoticNhds k P v := by
  have ‚ü®p‚ü© : Nonempty P := inferInstance
  nth_rw 1 [‚Üê asymptoticNhds_vadd_pure v p]
  simp_rw [‚Üê asymptoticNhds_vadd_pure v (u +·µ• p), vadd_pure, ‚Üê Filter.map_vadd, map_map]
  congr with v
  exact vadd_comm u v p

variable {Œ± : Type*} {l : Filter Œ±}

theorem _root_.Filter.Tendsto.asymptoticNhds_vadd_const {f : Œ± ‚Üí V} {v : V} (p : P)
    (hf : Tendsto f l (asymptoticNhds k V v)) :
    Tendsto (fun x => f x +·µ• p) l (asymptoticNhds k P v) := by
  rw [‚Üê asymptoticNhds_vadd_pure, vadd_pure]
  exact tendsto_map.comp hf

theorem _root_.Filter.Tendsto.const_vadd_asymptoticNhds {f : Œ± ‚Üí P} {v : V} (u : V)
    (hf : Tendsto f l (asymptoticNhds k P v)) :
    Tendsto (fun x => u +·µ• f x) l (asymptoticNhds k P v) := by
  rw [‚Üê vadd_asymptoticNhds u, ‚Üê Filter.map_vadd]
  exact tendsto_map.comp hf

variable [TopologicalSpace k] [OrderTopology k] [IsStrictOrderedRing k]
  [IsTopologicalAddGroup V] [ContinuousSMul k V]

theorem asymptoticNhds_eq_smul (v : V) : asymptoticNhds k V v = atTop (Œ± := k) ‚Ä¢ ùìù v := by
  unfold asymptoticNhds
  apply le_antisymm
  ¬∑ refine iSup_le fun u => ?_
    simp_rw [vadd_eq_add, add_pure, ‚Üê map‚ÇÇ_smul, map_map‚ÇÇ, ‚Üê map_prod_eq_map‚ÇÇ]
    have : (fun x : k √ó V => x.1 ‚Ä¢ x.2 + u) =·∂†[atTop √óÀ¢ ùìù v]
        (Function.uncurry (¬∑ ‚Ä¢ ¬∑)) ‚àò (fun x : k √ó V => (x.1, x.2 + x.1‚Åª¬π ‚Ä¢ u)) := by
      filter_upwards [tendsto_fst.eventually (eventually_ne_atTop 0)] with _ h
      simp [h]
    rw [map_congr this, ‚Üê map_map]
    apply map_mono
    have : Tendsto (fun x : k √ó V => (x.1, x.2 + x.1‚Åª¬π ‚Ä¢ u)) (atTop √óÀ¢ ùìù v) _ :=
      tendsto_fst.prodMk <| tendsto_snd.add <| tendsto_fst.inv_tendsto_atTop.smul_const u
    simpa
  ¬∑ apply (le_iSup _ 0).trans'
    simp

theorem asymptoticNhds_eq_smul_vadd (v : V) (p : P) :
    asymptoticNhds k P v = atTop (Œ± := k) ‚Ä¢ ùìù v +·µ• pure p := by
  rw [‚Üê asymptoticNhds_eq_smul, asymptoticNhds_vadd_pure]

instance {v : V} : (asymptoticNhds k P v).NeBot := by
  have ‚ü®p‚ü© : Nonempty P := inferInstance
  rw [asymptoticNhds_eq_smul_vadd v p]
  infer_instance

private theorem asymptoticNhds_zero' : asymptoticNhds k V (0 : V) = ‚ä§ := by
  rw [‚Üê top_le_iff, ‚Üê iSup_pure_eq_top, iSup_le_iff]
  intro v
  rw [‚Üê map_const (f := atTop (Œ± := k))]
  have : (fun _ => v) =·∂†[atTop (Œ± := k)]
      (Function.uncurry (¬∑ ‚Ä¢ ¬∑)) ‚àò (fun c => (c, c‚Åª¬π ‚Ä¢ v)) := by
    filter_upwards [eventually_ne_atTop 0] with _ h
    simp [h]
  rw [map_congr this, ‚Üê map_map, asymptoticNhds_eq_smul, ‚Üê map‚ÇÇ_smul, ‚Üê map_prod_eq_map‚ÇÇ]
  apply map_mono
  have : Tendsto (fun c => (c, c‚Åª¬π ‚Ä¢ v)) (atTop (Œ± := k)) _ :=
    tendsto_id.prodMk <| tendsto_inv_atTop_zero.smul_const v
  simpa

@[simp]
theorem asymptoticNhds_zero : asymptoticNhds k P (0 : V) = ‚ä§ := by
  have ‚ü®p‚ü© : Nonempty P := inferInstance
  rw [‚Üê asymptoticNhds_vadd_pure 0 p, asymptoticNhds_zero', vadd_pure]
  exact (Equiv.vaddConst p).surjective.filter_map_top

theorem _root_.Filter.Tendsto.atTop_smul_nhds_tendsto_asymptoticNhds {f : Œ± ‚Üí k} {g : Œ± ‚Üí V} {v : V}
    (hf : Tendsto f l atTop) (hg : Tendsto g l (ùìù v)) :
    Tendsto (fun x => f x ‚Ä¢ g x) l (asymptoticNhds k V v) := by
  rw [asymptoticNhds_eq_smul, ‚Üê map‚ÇÇ_smul, ‚Üê map_prod_eq_map‚ÇÇ]
  exact tendsto_map.comp (hf.prodMk hg)

theorem _root_.Filter.Tendsto.atTop_smul_const_tendsto_asymptoticNhds {f : Œ± ‚Üí k} (v : V)
    (hf : Tendsto f l atTop) :
    Tendsto (fun x => f x ‚Ä¢ v) l (asymptoticNhds k V v) :=
  hf.atTop_smul_nhds_tendsto_asymptoticNhds tendsto_const_nhds

theorem asymptoticNhds_smul (v : V) {c : k} (hc : 0 < c) :
    asymptoticNhds k P (c ‚Ä¢ v) = asymptoticNhds k P v := by
  have ‚ü®p‚ü© : Nonempty P := inferInstance
  simp_rw [asymptoticNhds_eq_smul_vadd _ p,
    ‚Üê show map (c ‚Ä¢ ¬∑) (ùìù v) = ùìù (c ‚Ä¢ v) from
      (Homeomorph.smulOfNeZero c hc.ne').map_nhds_eq v,
    ‚Üê map‚ÇÇ_smul, map‚ÇÇ_map_right, smul_smul, ‚Üê map‚ÇÇ_map_left,
    show map (¬∑ * c) atTop = atTop from (OrderIso.mulRight‚ÇÄ _ hc).map_atTop]

@[simp]
theorem nhds_bind_asymptoticNhds (v : V) :
    (ùìù v).bind (asymptoticNhds k P) = asymptoticNhds k P v := by
  apply le_antisymm
  ¬∑ have ‚ü®p‚ü© : Nonempty P := inferInstance
    eta_expand
    simp_rw [asymptoticNhds_eq_smul_vadd _ p, vadd_pure]
    nth_rw 2 [‚Üê nhds_bind_nhds]
    simp only [le_def, mem_map, ‚Üê map‚ÇÇ_smul, mem_map‚ÇÇ_iff, mem_bind]
    grind
  ¬∑ rw [‚Üê pure_bind v (asymptoticNhds k P)]
    exact bind_mono (pure_le_nhds v) .rfl

@[simp]
theorem asymptoticNhds_bind_nhds [TopologicalSpace P] [IsTopologicalAddTorsor P] (v : V) :
    (asymptoticNhds k P v).bind ùìù = asymptoticNhds k P v := by
  refine le_antisymm (fun s h => ?_) (bind_mono le_rfl (.of_forall pure_le_nhds))
  have ‚ü®p‚ü© : Nonempty P := inferInstance
  rw [asymptoticNhds_eq_smul_vadd _ p, vadd_pure] at h ‚ä¢
  rw [‚Üê nhds_bind_nhds] at h
  obtain ‚ü®t‚ÇÅ, ht‚ÇÅ, t‚ÇÇ, ht‚ÇÇ, hs‚ü© := h
  rw [mem_bind] at ht‚ÇÇ
  obtain ‚ü®t‚ÇÉ, ht‚ÇÉ, ht‚ÇÇ‚ü© := ht‚ÇÇ
  rw [bind_map, mem_bind]
  refine ‚ü®(t‚ÇÅ ‚à© Set.Ioi 0) ‚Ä¢ t‚ÇÉ, smul_mem_smul (inter_mem ht‚ÇÅ (Ioi_mem_atTop _)) ht‚ÇÉ,
    Set.forall_mem_image2.mpr fun c ‚ü®hc‚ÇÅ, hc‚ÇÇ‚ü© u hu => ?_‚ü©
  rw [show s = (¬∑ -·µ• p) ‚Åª¬π' ((¬∑ +·µ• p) ‚Åª¬π' s) by simp [Set.preimage_preimage]]
  apply tendsto_id.vsub tendsto_const_nhds
  rw [vadd_vsub]
  filter_upwards [smul_mem_nhds_smul‚ÇÄ hc‚ÇÇ.ne' (ht‚ÇÇ u hu)]
  rw [‚Üê Set.image_smul, Set.forall_mem_image]
  exact fun w hw => hs (Set.smul_mem_smul hc‚ÇÅ hw)

@[simp]
theorem asymptoticNhds_bind_asymptoticNhds (v : V) :
    (asymptoticNhds k V v).bind (asymptoticNhds k P) = asymptoticNhds k P v := by
  refine Filter.ext' fun p => ?_
  rw [asymptoticNhds_eq_smul, eventually_bind, ‚Üê map‚ÇÇ_smul, ‚Üê map_prod_eq_map‚ÇÇ, eventually_map,
    ‚Üê nhds_bind_asymptoticNhds, eventually_bind]
  nth_rw 2 [‚Üê map_snd_prod (atTop (Œ± := k)) (ùìù v)]
  rw [eventually_map]
  apply eventually_congr
  filter_upwards [tendsto_fst.eventually (eventually_gt_atTop 0)] with ‚ü®c, u‚ü© (hc : 0 < c)
  simp only [asymptoticNhds_smul _ hc]

end AffineSpace

open AffineSpace

variable (k) in
/-- The set of directions `v` for which the set has points arbitrarily far in directions near `v`.
-/
def asymptoticCone (s : Set P) : Set V := {v | ‚àÉ·∂† p in asymptoticNhds k P v, p ‚àà s}

theorem mem_asymptoticCone_iff {v : V} {s : Set P} :
    v ‚àà asymptoticCone k s ‚Üî ‚àÉ·∂† p in asymptoticNhds k P v, p ‚àà s :=
  Iff.rfl

@[simp]
theorem asymptoticCone_empty : asymptoticCone k (‚àÖ : Set P) = ‚àÖ :=
  Set.eq_empty_iff_forall_notMem.mpr fun _ => frequently_false _

@[gcongr]
theorem asymptoticCone_mono {s t : Set P} (h : s ‚äÜ t) : asymptoticCone k s ‚äÜ asymptoticCone k t :=
  fun _ h' => h'.mono h

theorem asymptoticCone_union {s t : Set P} :
    asymptoticCone k (s ‚à™ t) = asymptoticCone k s ‚à™ asymptoticCone k t := by
  ext
  simp only [Set.mem_union, mem_asymptoticCone_iff, Filter.frequently_or_distrib]

theorem asymptoticCone_biUnion {Œπ : Type*} {s : Set Œπ} (hs : s.Finite) (f : Œπ ‚Üí Set P) :
    asymptoticCone k (‚ãÉ i ‚àà s, f i) = ‚ãÉ i ‚àà s, asymptoticCone k (f i) := by
  induction s, hs using Set.Finite.induction_on <;>
    simp [asymptoticCone_union, *]

theorem asymptoticCone_sUnion {S : Set (Set P)} (hS : S.Finite) :
    asymptoticCone k (‚ãÉ‚ÇÄ S) = ‚ãÉ s ‚àà S, asymptoticCone k s := by
  rw [Set.sUnion_eq_biUnion, asymptoticCone_biUnion hS]

nonrec theorem Finset.asymptoticCone_biUnion {Œπ : Type*} (s : Finset Œπ) (f : Œπ ‚Üí Set P) :
    asymptoticCone k (‚ãÉ i ‚àà s, f i) = ‚ãÉ i ‚àà s, asymptoticCone k (f i) :=
  asymptoticCone_biUnion s.finite_toSet f

theorem asymptoticCone_iUnion_of_finite {Œπ : Type*} [Finite Œπ] (f : Œπ ‚Üí Set P) :
    asymptoticCone k (‚ãÉ i, f i) = ‚ãÉ i, asymptoticCone k (f i) := by
  rw [‚Üê Set.sUnion_range, asymptoticCone_sUnion (Set.finite_range f), Set.biUnion_range]

variable [TopologicalSpace k] [OrderTopology k] [IsStrictOrderedRing k]
  [IsTopologicalAddGroup V] [ContinuousSMul k V]

theorem zero_mem_asymptoticCone {s : Set P} : 0 ‚àà asymptoticCone k s ‚Üî s.Nonempty := by
  refine ‚ü®Function.mtr ?_, fun _ => ?_‚ü©
  ¬∑ simp +contextual [Set.not_nonempty_iff_eq_empty]
  ¬∑ simpa [mem_asymptoticCone_iff]

theorem asymptoticCone_nonempty {s : Set P} : (asymptoticCone k s).Nonempty ‚Üî s.Nonempty := by
  refine ‚ü®Function.mtr ?_, fun h => ‚ü®0, zero_mem_asymptoticCone.mpr h‚ü©‚ü©
  simp +contextual [Set.not_nonempty_iff_eq_empty]

@[simp]
theorem smul_mem_asymptoticCone_iff {s : Set P} {c : k} {v : V} (hc : 0 < c) :
    c ‚Ä¢ v ‚àà asymptoticCone k s ‚Üî v ‚àà asymptoticCone k s := by
  simp_rw [mem_asymptoticCone_iff, asymptoticNhds_smul v hc]

theorem smul_mem_asymptoticCone {s : Set P} {c : k} {v : V} (hc : 0 ‚â§ c)
    (h : v ‚àà asymptoticCone k s) : c ‚Ä¢ v ‚àà asymptoticCone k s := by
  rcases hc.eq_or_lt with rfl | hc
  ¬∑ rw [zero_smul, zero_mem_asymptoticCone, ‚Üê asymptoticCone_nonempty (k := k)]; exact ‚ü®v, h‚ü©
  ¬∑ rwa [smul_mem_asymptoticCone_iff hc]

theorem asymptoticCone_eq_closure_of_forall_smul_mem {s : Set V}
    (hs : ‚àÄ c : k, 0 < c ‚Üí ‚àÄ x ‚àà s, c ‚Ä¢ x ‚àà s) : asymptoticCone k s = closure s := by
  ext v
  rw [mem_closure_iff_frequently, ‚Üê map_snd_prod (atTop (Œ± := k)) (ùìù v), frequently_map,
    mem_asymptoticCone_iff, asymptoticNhds_eq_smul, ‚Üê map‚ÇÇ_smul, ‚Üê map_prod_eq_map‚ÇÇ, frequently_map]
  apply frequently_congr
  filter_upwards [tendsto_fst.eventually (eventually_gt_atTop 0)] with ‚ü®c, u‚ü© hc
  refine ‚ü®fun hu => ?_, hs c hc u‚ü©
  specialize hs c‚Åª¬π (inv_pos_of_pos hc) (c ‚Ä¢ u) hu
  rwa [inv_smul_smul‚ÇÄ hc.ne'] at hs

theorem asymptoticCone_submodule {s : Submodule k V} : asymptoticCone k (s : Set V) = closure s :=
  asymptoticCone_eq_closure_of_forall_smul_mem fun _ _ _ h => s.smul_mem _ h

theorem asymptoticCone_affineSubspace {s : AffineSubspace k P} (hs : (s : Set P).Nonempty) :
    asymptoticCone k (s : Set P) = closure s.direction := by
  have ‚ü®p, hp‚ü© := hs
  ext v
  simp_rw [‚Üê asymptoticCone_submodule, mem_asymptoticCone_iff, ‚Üê asymptoticNhds_vadd_pure v p,
    vadd_pure, frequently_map, SetLike.mem_coe, s.vadd_mem_iff_mem_direction _ hp]

@[simp]
theorem asymptoticCone_univ : asymptoticCone k (Set.univ : Set P) = Set.univ := by
  rw [‚Üê AffineSubspace.top_coe k, asymptoticCone_affineSubspace Set.univ_nonempty,
    AffineSubspace.direction_top, Submodule.top_coe, closure_univ]

theorem asymptoticCone_closure [TopologicalSpace P] [IsTopologicalAddTorsor P] (s : Set P) :
    asymptoticCone k (closure s) = asymptoticCone k s := by
  ext
  simp_rw [mem_asymptoticCone_iff, mem_closure_iff_frequently, ‚Üê frequently_bind,
    asymptoticNhds_bind_nhds]

theorem isClosed_asymptoticCone {s : Set P} : IsClosed (asymptoticCone k s) := by
  have ‚ü®p‚ü© : Nonempty P := inferInstance
  rw [isClosed_iff_frequently]
  intro v h
  simp_rw [mem_asymptoticCone_iff, ‚Üê frequently_bind, nhds_bind_asymptoticNhds] at h
  exact h

@[simp]
theorem asymptoticCone_asymptoticCone (s : Set P) :
    asymptoticCone k (asymptoticCone k s) = asymptoticCone k s := by
  ext
  simp_rw [mem_asymptoticCone_iff, ‚Üê Filter.frequently_bind, asymptoticNhds_bind_asymptoticNhds]

end General

section Convex

open AffineSpace

variable
  {k V : Type*}
  [Field k] [LinearOrder k] [IsStrictOrderedRing k] [TopologicalSpace k] [OrderTopology k]
  [AddCommGroup V] [Module k V] [TopologicalSpace V] [IsTopologicalAddGroup V] [ContinuousSMul k V]
  {s : Set V}

/-- If a closed set `s` is star-convex at `p` and `v` is in the asymptotic cone of `s`, then the ray
of direction `v` starting from `p` is contained in `s`. -/
theorem StarConvex.smul_vadd_mem_of_isClosed_of_mem_asymptoticCone {c : k} {v p : V}
    (hs‚ÇÅ : StarConvex k p s) (hs‚ÇÇ : IsClosed s) (hc : 0 ‚â§ c) (hv : v ‚àà asymptoticCone k s) :
    c ‚Ä¢ v +·µ• p ‚àà s := by
  refine isClosed_iff_frequently.mp hs‚ÇÇ _ <|
    tendsto_snd (f := atTop (Œ± := k)) |>.const_smul _ |>.vadd_const _ |>.frequently ?_
  rw [mem_asymptoticCone_iff, asymptoticNhds_eq_smul_vadd v p, vadd_pure, frequently_map,
    ‚Üê map‚ÇÇ_smul, ‚Üê map_prod_eq_map‚ÇÇ, frequently_map] at hv
  apply hv.mp
  filter_upwards [tendsto_fst.eventually (eventually_ge_atTop c)]
    with ‚ü®t, u‚ü© (ht : c ‚â§ t) (h : t ‚Ä¢ u +·µ• p ‚àà s)
  change c ‚Ä¢ u +·µ• p ‚àà s
  apply hs‚ÇÅ.segment_subset h
  simp_rw [mem_segment_iff_sameRay, ‚Üê vsub_eq_sub, vadd_vsub, vadd_vsub_vadd_cancel_right,
    ‚Üê sub_smul]
  exact (SameRay.sameRay_nonneg_smul_left _ hc).nonneg_smul_right (sub_nonneg.mpr ht)

/-- If `v` is in the asymptotic cone of a closed convex set `s`, then for every `p ‚àà s`, the ray of
direction `v` starting from `p` is contained in `s`. -/
theorem Convex.smul_vadd_mem_of_isClosed_of_mem_asymptoticCone {c : k} {v p : V}
    (hs‚ÇÅ : Convex k s) (hs‚ÇÇ : IsClosed s) (hc : 0 ‚â§ c) (hv : v ‚àà asymptoticCone k s) (hp : p ‚àà s) :
    c ‚Ä¢ v +·µ• p ‚àà s :=
  (hs‚ÇÅ hp).smul_vadd_mem_of_isClosed_of_mem_asymptoticCone hs‚ÇÇ hc hv

protected theorem Convex.asymptoticCone (hs : Convex k s) : Convex k (asymptoticCone k s) := by
  wlog hs' : IsClosed s generalizing s
  ¬∑ rw [‚Üê asymptoticCone_closure]; exact this hs.closure isClosed_closure
  rcases s.eq_empty_or_nonempty with rfl | ‚ü®p, hp‚ü©
  ¬∑ rw [asymptoticCone_empty]; exact convex_empty
  intro v hv u hu a b ha hb hab
  rw [mem_asymptoticCone_iff]
  refine tendsto_id.atTop_smul_const_tendsto_asymptoticNhds _ |>.asymptoticNhds_vadd_const p
    |>.frequently (Eventually.frequently ?_)
  filter_upwards [eventually_ge_atTop 0] with c hc
  simp_rw [id, smul_add, smul_smul]
  have h‚ÇÅ : c ‚Ä¢ v +·µ• p ‚àà s := hs.smul_vadd_mem_of_isClosed_of_mem_asymptoticCone hs' hc hv hp
  have h‚ÇÇ : c ‚Ä¢ u +·µ• p ‚àà s := hs.smul_vadd_mem_of_isClosed_of_mem_asymptoticCone hs' hc hu hp
  apply hs.segment_subset h‚ÇÅ h‚ÇÇ
  rw [‚Üê affineSegment_eq_segment, mem_vadd_const_affineSegment, affineSegment_eq_segment]
  exists a, b, ha, hb, hab
  module

/-- If `v` is in the asymptotic cone of a convex set `s`, then for every interior point `p`, the ray
of direction `v` starting from `p` is contained in `s`. -/
theorem Convex.smul_vadd_mem_of_mem_nhds_of_mem_asymptoticCone {c : k} {v p : V}
    (hs : Convex k s) (hc : 0 ‚â§ c) (hp : s ‚àà ùìù p) (hv : v ‚àà asymptoticCone k s) :
    c ‚Ä¢ v +·µ• p ‚àà s := by
  rw [mem_asymptoticCone_iff, asymptoticNhds_eq_smul_vadd v (c ‚Ä¢ v +·µ• p),  vadd_pure,
    frequently_map, ‚Üê map‚ÇÇ_smul, ‚Üê map_prod_eq_map‚ÇÇ, frequently_map] at hv
  refine frequently_const.mp (hv.mp ?_)
  have : Tendsto (fun u => - (c ‚Ä¢ u : V) +·µ• c ‚Ä¢ v +·µ• p) (ùìù v) (ùìù p) :=
    Continuous.tendsto' (by fun_prop) _ _ (by simp)
  filter_upwards [tendsto_fst.eventually <| eventually_gt_atTop 0, this.comp tendsto_snd hp]
    with ‚ü®t, u‚ü© (ht : 0 < t) (hu : - (c ‚Ä¢ u) +·µ• c ‚Ä¢ v +·µ• p ‚àà s) (h : t ‚Ä¢ u +·µ• c ‚Ä¢ v +·µ• p ‚àà s)
  apply hs.segment_subset hu h
  simp_rw [mem_segment_iff_sameRay, ‚Üê vsub_eq_sub]
  rw [vsub_vadd_eq_vsub_sub, vsub_self, zero_sub, neg_neg, vadd_vsub]
  exact (SameRay.sameRay_nonneg_smul_left _ hc).pos_smul_right ht

end Convex
