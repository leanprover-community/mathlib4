/-
Copyright (c) 2025 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/
module

public import Mathlib.Algebra.BigOperators.Group.Finset.Interval
public import Mathlib.Analysis.Normed.Group.Int
public import Mathlib.Analysis.Normed.Group.Uniform
public import Mathlib.Analysis.Normed.MulAction
public import Mathlib.Order.Filter.AtTopBot.Interval
public import Mathlib.Topology.Algebra.InfiniteSum.Defs


/-!
# Sums over symmetric integer intervals

This file contains some lemmas about sums over symmetric integer intervals `Ixx -N N` used, for
example in the definition of the Eisenstein series `E2`.
In particular we define `symmetricIcc`, `symmetricIco`, `symmetricIoc` and `symmetricIoo` as
`SummationFilter`s corresponding to the intervals `Icc -N N`, `Ico -N N`, `Ioc -N N` respectively.
We also prove that these filters are all `NeBot` and `LeAtTop`.

-/

@[expose] public section

open Finset Topology Function Filter SummationFilter

namespace SummationFilter

section IntervalFilters

variable (G : Type*) [Neg G] [Preorder G] [LocallyFiniteOrder G]

/-- The SummationFilter on a locally finite order `G` corresponding to the symmetric
intervals `Icc (-N) N`¬∑ -/
@[simps]
def symmetricIcc : SummationFilter G where
  filter := atTop.map (fun g ‚Ü¶ Icc (-g) g)

/-- The SummationFilter on a locally finite order `G` corresponding to the symmetric
intervals `Ioo (-N) N`¬∑ Note that for `G = ‚Ñ§` this coincides with
`symmetricIcc` so one should use that. See `symmetricIcc_eq_symmetricIoo_int`. -/
@[simps]
def symmetricIoo : SummationFilter G where
  filter := atTop.map (fun g ‚Ü¶ Ioo (-g) g)

/-- The SummationFilter on a locally finite order `G` corresponding to the symmetric
intervals `Ico (-N) N`¬∑ -/
@[simps]
def symmetricIco : SummationFilter G where
  filter := atTop.map (fun N ‚Ü¶ Ico (-N) N)

/-- The SummationFilter on a locally finite order `G` corresponding to the symmetric
intervals `Ioc (-N) N`¬∑ -/
@[simps]
def symmetricIoc : SummationFilter G where
  filter := atTop.map (fun N ‚Ü¶ Ioc (-N) N)

variable [(atTop : Filter G).NeBot]

instance : (symmetricIcc G).NeBot where
  ne_bot := by simp [symmetricIcc, Filter.NeBot.map]

instance : (symmetricIco G).NeBot where
  ne_bot := by simp [symmetricIco, Filter.NeBot.map]

instance : (symmetricIoc G).NeBot where
  ne_bot := by simp [symmetricIoc, Filter.NeBot.map]

instance : (symmetricIoo G).NeBot where
  ne_bot := by simp [symmetricIoo, Filter.NeBot.map]

section LeAtTop

variable {G : Type*} [AddCommGroup G] [PartialOrder G] [IsOrderedAddMonoid G] [LocallyFiniteOrder G]

lemma symmetricIcc_le_Conditional :
    (symmetricIcc G).filter ‚â§ (conditional G).filter :=
  Filter.map_mono (tendsto_neg_atTop_atBot.prodMk tendsto_id)

instance : (symmetricIcc G).LeAtTop where
  le_atTop := le_trans symmetricIcc_le_Conditional (conditional G).le_atTop

variable [NoTopOrder G] [NoBotOrder G]

instance : (symmetricIco G).LeAtTop where
  le_atTop := by
    rw [symmetricIco, map_le_iff_le_comap, ‚Üê @tendsto_iff_comap]
    exact tendsto_Ico_neg_atTop_atTop

instance : (symmetricIoc G).LeAtTop where
  le_atTop := by
    rw [symmetricIoc, map_le_iff_le_comap, ‚Üê @tendsto_iff_comap]
    exact tendsto_Ioc_neg_atTop_atTop

instance : (symmetricIoo G).LeAtTop where
  le_atTop := by
    rw [symmetricIoo, map_le_iff_le_comap, ‚Üê @tendsto_iff_comap]
    exact tendsto_Ioo_neg_atTop_atTop

end LeAtTop

end IntervalFilters
section Int

variable {Œ± : Type*} {f : ‚Ñ§ ‚Üí Œ±} [CommGroup Œ±] [TopologicalSpace Œ±] [ContinuousMul Œ±]

lemma symmetricIcc_eq_map_Icc_nat :
    (symmetricIcc ‚Ñ§).filter = atTop.map (fun N : ‚Ñï ‚Ü¶ Icc (-(N : ‚Ñ§)) N) := by
  simp [‚Üê Nat.map_cast_int_atTop, Function.comp_def]

lemma symmetricIcc_eq_symmetricIoo_int : symmetricIcc ‚Ñ§ = symmetricIoo ‚Ñ§ := by
  simp only [symmetricIcc, symmetricIoo, mk.injEq]
  ext s
  simp only [‚Üê Nat.map_cast_int_atTop, Filter.map_map, Filter.mem_map, mem_atTop_sets, ge_iff_le,
    Set.mem_preimage, comp_apply]
  refine ‚ü®fun ‚ü®a, ha‚ü© ‚Ü¶ ‚ü®a + 1, fun b hb ‚Ü¶ ?_‚ü©, fun ‚ü®a, ha‚ü© ‚Ü¶ ‚ü®a - 1, fun b hb ‚Ü¶ ?_‚ü©‚ü© <;>
  [convert ha (b - 1) (by grind) using 1; convert ha (b + 1) (by grind) using 1] <;>
  simpa [Finset.ext_iff] using by grind

@[to_additive]
lemma _root_.HasProd.hasProd_symmetricIco_of_hasProd_symmetricIcc {a : Œ±}
    (hf : HasProd f a (symmetricIcc ‚Ñ§)) (hf2 : Tendsto (fun N : ‚Ñï ‚Ü¶ (f N)‚Åª¬π) atTop (ùìù 1)) :
    HasProd f a (symmetricIco ‚Ñ§) := by
  simp only [HasProd, tendsto_map'_iff, symmetricIcc_eq_map_Icc_nat,
    ‚Üê Nat.map_cast_int_atTop, symmetricIco] at *
  apply tendsto_of_div_tendsto_one _ hf
  simpa [Pi.div_def, fun N : ‚Ñï ‚Ü¶ prod_Icc_eq_prod_Ico_mul f (show (-N : ‚Ñ§) ‚â§ N by lia)]
    using hf2

@[deprecated (since := "2025-12-15")]
alias HasProd.hasProd_symmetricIco_of_hasProd_symmetricIcc :=
  _root_.HasProd.hasProd_symmetricIco_of_hasProd_symmetricIcc

@[to_additive]
lemma multipliable_symmetricIco_of_multipliable_symmetricIcc
    (hf : Multipliable f (symmetricIcc ‚Ñ§)) (hf2 : Tendsto (fun N : ‚Ñï ‚Ü¶ (f N)‚Åª¬π) atTop (ùìù 1)) :
    Multipliable f (symmetricIco ‚Ñ§) :=
  (hf.hasProd.hasProd_symmetricIco_of_hasProd_symmetricIcc hf2).multipliable

@[deprecated (since := "2025-12-15")]
alias multipliable_symmetricIco_of_multiplible_symmetricIcc :=
  multipliable_symmetricIco_of_multipliable_symmetricIcc

@[to_additive]
lemma tprod_symmetricIcc_eq_tprod_symmetricIco [T2Space Œ±]
    (hf : Multipliable f (symmetricIcc ‚Ñ§)) (hf2 : Tendsto (fun N : ‚Ñï ‚Ü¶ (f N)‚Åª¬π) atTop (ùìù 1)) :
    ‚àè'[symmetricIco ‚Ñ§] b, f b = ‚àè'[symmetricIcc ‚Ñ§] b, f b :=
  (hf.hasProd.hasProd_symmetricIco_of_hasProd_symmetricIcc hf2).tprod_eq

@[to_additive]
lemma hasProd_symmetricIcc_iff {Œ± : Type*} [CommMonoid Œ±] [TopologicalSpace Œ±]
    {f : ‚Ñ§ ‚Üí Œ±} {a : Œ±} : HasProd f a (symmetricIcc ‚Ñ§) ‚Üî
    Tendsto (fun N : ‚Ñï ‚Ü¶ ‚àè n ‚àà Icc (-(N : ‚Ñ§)) N, f n) atTop (ùìù a) := by
  simp [HasProd, symmetricIcc, ‚Üê Nat.map_cast_int_atTop, comp_def]

@[to_additive]
lemma hasProd_symmetricIco_int_iff {Œ± : Type*} [CommMonoid Œ±] [TopologicalSpace Œ±]
    {f : ‚Ñ§ ‚Üí Œ±} {a : Œ±} : HasProd f a (symmetricIco ‚Ñ§) ‚Üî
    Tendsto (fun N : ‚Ñï ‚Ü¶ ‚àè n ‚àà Ico (-(N : ‚Ñ§)) (N : ‚Ñ§), f n) atTop (ùìù a) := by
  simp [HasProd, symmetricIco, ‚Üê Nat.map_cast_int_atTop, comp_def]

@[to_additive]
lemma hasProd_symmetricIoc_int_iff {Œ± : Type*} [CommMonoid Œ±] [TopologicalSpace Œ±]
    {f : ‚Ñ§ ‚Üí Œ±} {a : Œ±} : HasProd f a (symmetricIoc ‚Ñ§) ‚Üî
    Tendsto (fun N : ‚Ñï ‚Ü¶ ‚àè n ‚àà Ioc (-(N : ‚Ñ§)) (N : ‚Ñ§), f n) atTop (ùìù a) := by
  simp [HasProd, symmetricIoc, ‚Üê Nat.map_cast_int_atTop, comp_def]

lemma _root_.Summable.tendsto_zero_of_even_summable_symmetricIcc {F : Type*} [NormedAddCommGroup F]
    [NormSMulClass ‚Ñ§ F] {f : ‚Ñ§ ‚Üí F} (hf : Summable f (symmetricIcc ‚Ñ§)) (hs : f.Even) :
    Tendsto f atTop (ùìù 0) := by
  rw [tendsto_zero_iff_norm_tendsto_zero]
  obtain ‚ü®L, hL‚ü© := hf
  rw [HasSum, symmetricIcc_filter, tendsto_map'_iff, Function.comp_def] at hL
  have := hL.sub (hL.comp (tendsto_atTop_add_const_right _ (-1) tendsto_id))
  simp only [id_eq, Int.reduceNeg, Function.comp_apply, sub_self, ‚Üê sub_eq_add_neg] at this
  rw [tendsto_zero_iff_norm_tendsto_zero] at this
  refine (mul_zero (_ : ‚Ñù) ‚ñ∏ this.const_mul 2‚Åª¬π).congr' ?_
  filter_upwards [eventually_ge_atTop 1] with x hx
  have : Finset.Icc (-x) x = Icc (-(x - 1)) (x - 1) ‚à™ {-x, x} := by
    lift x to ‚Ñï using by positivity
    convert Finset.Icc_succ_succ (x - 1) (x - 1) <;> grind
  rw [this, Finset.sum_union, Finset.sum_insert, Finset.sum_singleton,
    hs x, add_comm, add_sub_cancel_right, ‚Üê two_zsmul, norm_smul, Int.norm_eq_abs,
    Int.cast_two, abs_two, inv_mul_cancel_left‚ÇÄ two_ne_zero] <;>
  ¬∑ simp only [disjoint_iff_ne, mem_insert, mem_singleton, mem_Icc]
    omega

end Int

end SummationFilter
