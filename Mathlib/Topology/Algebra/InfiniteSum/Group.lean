/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl
-/
import Mathlib.SetTheory.Cardinal.Finite
import Mathlib.Topology.Algebra.InfiniteSum.Basic
import Mathlib.Topology.UniformSpace.Cauchy
import Mathlib.Topology.Algebra.IsUniformGroup.Defs
import Mathlib.Topology.Algebra.Group.Pointwise

/-!
# Infinite sums and products in topological groups

Lemmas on topological sums in groups (as opposed to monoids).
-/

noncomputable section

open Filter Finset Function

open scoped Topology

variable {Œ± Œ≤ Œ≥ : Type*} {L : SummationFilter Œ≤}

section IsTopologicalGroup

variable [CommGroup Œ±] [TopologicalSpace Œ±] [IsTopologicalGroup Œ±]
variable {f g : Œ≤ ‚Üí Œ±} {a a‚ÇÅ a‚ÇÇ : Œ±}

-- `by simpa using` speeds up elaboration. Why?
@[to_additive]
theorem HasProd.inv (h : HasProd f a L) : HasProd (fun b ‚Ü¶ (f b)‚Åª¬π) a‚Åª¬π L := by
  simpa only using h.map (MonoidHom.id Œ±)‚Åª¬π continuous_inv

@[to_additive]
theorem Multipliable.inv (hf : Multipliable f L) : Multipliable (fun b ‚Ü¶ (f b)‚Åª¬π) L :=
  hf.hasProd.inv.multipliable

@[to_additive]
theorem Multipliable.of_inv (hf : Multipliable (fun b ‚Ü¶ (f b)‚Åª¬π) L) : Multipliable f L := by
  simpa only [inv_inv] using hf.inv

@[to_additive]
theorem multipliable_inv_iff : (Multipliable (fun b ‚Ü¶ (f b)‚Åª¬π) L) ‚Üî Multipliable f L:=
  ‚ü®Multipliable.of_inv, Multipliable.inv‚ü©

@[to_additive]
theorem HasProd.div (hf : HasProd f a‚ÇÅ L) (hg : HasProd g a‚ÇÇ L) :
    HasProd (fun b ‚Ü¶ f b / g b) (a‚ÇÅ / a‚ÇÇ) L := by
  simp only [div_eq_mul_inv]
  exact hf.mul hg.inv

@[to_additive]
theorem Multipliable.div (hf : Multipliable f L) (hg : Multipliable g L) :
    Multipliable (fun b ‚Ü¶ f b / g b) L :=
  (hf.hasProd.div hg.hasProd).multipliable

@[to_additive]
theorem Multipliable.trans_div (hg : Multipliable g L) (hfg : Multipliable (fun b ‚Ü¶ f b / g b) L) :
    Multipliable f L := by
  simpa only [div_mul_cancel] using hfg.mul hg

@[to_additive]
theorem multipliable_iff_of_multipliable_div (hfg : Multipliable (fun b ‚Ü¶ f b / g b) L) :
    Multipliable f L ‚Üî Multipliable g L :=
  ‚ü®fun hf ‚Ü¶ hf.trans_div <| by simpa only [inv_div] using hfg.inv, fun hg ‚Ü¶ hg.trans_div hfg‚ü©

@[to_additive]
theorem HasProd.update [L.LeAtTop] (hf : HasProd f a‚ÇÅ L) (b : Œ≤) [DecidableEq Œ≤] (a : Œ±) :
    HasProd (update f b a) (a / f b * a‚ÇÅ) L := by
  convert (hasProd_ite_eq b (a / f b) (L := L)).mul hf with b'
  by_cases h : b' = b
  ¬∑ rw [h, update_self]
    simp
  ¬∑ simp only [h, update_of_ne, if_false, Ne, one_mul, not_false_iff]

@[to_additive]
theorem Multipliable.update [L.LeAtTop] (hf : Multipliable f L) (b : Œ≤) [DecidableEq Œ≤] (a : Œ±) :
    Multipliable (update f b a) L :=
  (hf.hasProd.update b a).multipliable

@[to_additive]
theorem HasProd.hasProd_compl_iff {s : Set Œ≤} (hf : HasProd (f ‚àò (‚Üë) : s ‚Üí Œ±) a‚ÇÅ) :
    HasProd (f ‚àò (‚Üë) : ‚Üës·∂ú ‚Üí Œ±) a‚ÇÇ ‚Üî HasProd f (a‚ÇÅ * a‚ÇÇ) := by
  refine ‚ü®fun h ‚Ü¶ hf.mul_compl h, fun h ‚Ü¶ ?_‚ü©
  rw [hasProd_subtype_iff_mulIndicator] at hf ‚ä¢
  rw [Set.mulIndicator_compl]
  simpa only [div_eq_mul_inv, mul_inv_cancel_comm] using h.div hf

@[to_additive]
theorem HasProd.hasProd_iff_compl {s : Set Œ≤} (hf : HasProd (f ‚àò (‚Üë) : s ‚Üí Œ±) a‚ÇÅ) :
    HasProd f a‚ÇÇ ‚Üî HasProd (f ‚àò (‚Üë) : ‚Üës·∂ú ‚Üí Œ±) (a‚ÇÇ / a‚ÇÅ) :=
  Iff.symm <| hf.hasProd_compl_iff.trans <| by rw [mul_div_cancel]

@[to_additive]
theorem Multipliable.multipliable_compl_iff {s : Set Œ≤} (hf : Multipliable (f ‚àò (‚Üë) : s ‚Üí Œ±)) :
    Multipliable (f ‚àò (‚Üë) : ‚Üës·∂ú ‚Üí Œ±) ‚Üî Multipliable f where
  mp := fun ‚ü®_, ha‚ü© ‚Ü¶ (hf.hasProd.hasProd_compl_iff.1 ha).multipliable
  mpr := fun ‚ü®_, ha‚ü© ‚Ü¶ (hf.hasProd.hasProd_iff_compl.1 ha).multipliable

@[to_additive]
protected theorem Finset.hasProd_compl_iff (s : Finset Œ≤) :
    HasProd (fun x : { x // x ‚àâ s } ‚Ü¶ f x) a ‚Üî HasProd f (a * ‚àè i ‚àà s, f i) :=
  (s.hasProd f).hasProd_compl_iff.trans <| by rw [mul_comm]

@[to_additive]
protected theorem Finset.hasProd_iff_compl (s : Finset Œ≤) :
    HasProd f a ‚Üî HasProd (fun x : { x // x ‚àâ s } ‚Ü¶ f x) (a / ‚àè i ‚àà s, f i) :=
  (s.hasProd f).hasProd_iff_compl

@[to_additive]
protected theorem Finset.multipliable_compl_iff (s : Finset Œ≤) :
    (Multipliable fun x : { x // x ‚àâ s } ‚Ü¶ f x) ‚Üî Multipliable f :=
  (s.multipliable f).multipliable_compl_iff

@[to_additive]
theorem Set.Finite.multipliable_compl_iff {s : Set Œ≤} (hs : s.Finite) :
    Multipliable (f ‚àò (‚Üë) : ‚Üës·∂ú ‚Üí Œ±) ‚Üî Multipliable f :=
  (hs.multipliable f).multipliable_compl_iff

@[to_additive]
theorem hasProd_ite_div_hasProd [L.LeAtTop] [DecidableEq Œ≤] (hf : HasProd f a L) (b : Œ≤) :
    HasProd (fun n ‚Ü¶ ite (n = b) 1 (f n)) (a / f b) L := by
  convert hf.update b 1 using 1
  ¬∑ ext n
    rw [Function.update_apply]
  ¬∑ rw [div_mul_eq_mul_div, one_mul]

/-- A more general version of `Multipliable.congr`, allowing the functions to
disagree on a finite set.

Note that this requires the target to be a group, and hence fails for products valued
in a ring. See `Multipliable.congr_cofinite‚ÇÄ` for a version applying in this case,
with an additional non-vanishing hypothesis.
-/
@[to_additive /-- A more general version of `Summable.congr`, allowing the functions to
disagree on a finite set. -/]
theorem Multipliable.congr_cofinite (hf : Multipliable f) (hfg : f =·∂†[cofinite] g) :
    Multipliable g :=
  hfg.multipliable_compl_iff.mp <| (hfg.multipliable_compl_iff.mpr hf).congr (by simp)

/-- A more general version of `multipliable_congr`, allowing the functions to
disagree on a finite set. -/
@[to_additive /-- A more general version of `summable_congr`, allowing the functions to
disagree on a finite set. -/]
theorem multipliable_congr_cofinite (hfg : f =·∂†[cofinite] g) :
    Multipliable f ‚Üî Multipliable g :=
  ‚ü®fun h ‚Ü¶ h.congr_cofinite hfg, fun h ‚Ü¶ h.congr_cofinite (hfg.mono fun _ h' ‚Ü¶ h'.symm)‚ü©

@[to_additive]
theorem Multipliable.congr_atTop {f‚ÇÅ g‚ÇÅ : ‚Ñï ‚Üí Œ±} (hf : Multipliable f‚ÇÅ) (hfg : f‚ÇÅ =·∂†[atTop] g‚ÇÅ) :
    Multipliable g‚ÇÅ := hf.congr_cofinite (Nat.cofinite_eq_atTop ‚ñ∏ hfg)

@[to_additive]
theorem multipliable_congr_atTop {f‚ÇÅ g‚ÇÅ : ‚Ñï ‚Üí Œ±} (hfg : f‚ÇÅ =·∂†[atTop] g‚ÇÅ) :
    Multipliable f‚ÇÅ ‚Üî Multipliable g‚ÇÅ := multipliable_congr_cofinite (Nat.cofinite_eq_atTop ‚ñ∏ hfg)

section tprod

variable [T2Space Œ±]

@[to_additive]
theorem tprod_inv : ‚àè'[L] b, (f b)‚Åª¬π = (‚àè'[L] b, f b)‚Åª¬π :=
  ((Homeomorph.inv Œ±).isClosedEmbedding.map_tprod f (g := MulEquiv.inv Œ±)).symm

@[to_additive]
protected theorem Multipliable.tprod_div [L.NeBot] (hf : Multipliable f L) (hg : Multipliable g L) :
    ‚àè'[L] b, (f b / g b) = (‚àè'[L] b, f b) / ‚àè'[L] b, g b :=
  (hf.hasProd.div hg.hasProd).tprod_eq

@[to_additive]
protected theorem Multipliable.prod_mul_tprod_compl {s : Finset Œ≤} (hf : Multipliable f) :
    (‚àè x ‚àà s, f x) * ‚àè' x : ‚Üë(s : Set Œ≤)·∂ú, f x = ‚àè' x, f x :=
  ((s.hasProd f).mul_compl (s.multipliable_compl_iff.2 hf).hasProd).tprod_eq.symm

/-- Let `f : Œ≤ ‚Üí Œ±` be a multipliable function and let `b ‚àà Œ≤` be an index.
Lemma `tprod_eq_mul_tprod_ite` writes `‚àè n, f n` as `f b` times the product of the
remaining terms. -/
@[to_additive /-- Let `f : Œ≤ ‚Üí Œ±` be a summable function and let `b ‚àà Œ≤` be an index.
Lemma `tsum_eq_add_tsum_ite` writes `Œ£' n, f n` as `f b` plus the sum of the
remaining terms. -/]
protected theorem Multipliable.tprod_eq_mul_tprod_ite [DecidableEq Œ≤] (hf : Multipliable f)
    (b : Œ≤) : ‚àè' n, f n = f b * ‚àè' n, ite (n = b) 1 (f n) := by
  rw [(hasProd_ite_div_hasProd hf.hasProd b).tprod_eq]
  exact (mul_div_cancel _ _).symm

end tprod

end IsTopologicalGroup

section IsUniformGroup

variable [CommGroup Œ±] [UniformSpace Œ±]

/-- The **Cauchy criterion** for infinite products, also known as the **Cauchy convergence test** -/
@[to_additive /-- The **Cauchy criterion** for infinite sums, also known as the
**Cauchy convergence test** -/]
theorem multipliable_iff_cauchySeq_finset [CompleteSpace Œ±] {f : Œ≤ ‚Üí Œ±} :
    Multipliable f ‚Üî CauchySeq fun s : Finset Œ≤ ‚Ü¶ ‚àè b ‚àà s, f b := by
  classical exact cauchy_map_iff_exists_tendsto.symm

variable [IsUniformGroup Œ±] {f g : Œ≤ ‚Üí Œ±}

@[to_additive]
theorem cauchySeq_finset_iff_prod_vanishing :
    (CauchySeq fun s : Finset Œ≤ ‚Ü¶ ‚àè b ‚àà s, f b) ‚Üî
      ‚àÄ e ‚àà ùìù (1 : Œ±), ‚àÉ s : Finset Œ≤, ‚àÄ t, Disjoint t s ‚Üí (‚àè b ‚àà t, f b) ‚àà e := by
  classical
  simp only [CauchySeq, cauchy_map_iff, prod_atTop_atTop_eq,
    uniformity_eq_comap_nhds_one Œ±, tendsto_comap_iff, Function.comp_def, atTop_neBot, true_and]
  rw [tendsto_atTop']
  constructor
  ¬∑ intro h e he
    obtain ‚ü®‚ü®s‚ÇÅ, s‚ÇÇ‚ü©, h‚ü© := h e he
    use s‚ÇÅ ‚à™ s‚ÇÇ
    intro t ht
    specialize h (s‚ÇÅ ‚à™ s‚ÇÇ, s‚ÇÅ ‚à™ s‚ÇÇ ‚à™ t) ‚ü®le_sup_left, le_sup_of_le_left le_sup_right‚ü©
    simpa only [Finset.prod_union ht.symm, mul_div_cancel_left] using h
  ¬∑ rintro h e he
    rcases exists_nhds_split_inv he with ‚ü®d, hd, hde‚ü©
    rcases h d hd with ‚ü®s, h‚ü©
    use (s, s)
    rintro ‚ü®t‚ÇÅ, t‚ÇÇ‚ü© ‚ü®ht‚ÇÅ, ht‚ÇÇ‚ü©
    have : ((‚àè b ‚àà t‚ÇÇ, f b) / ‚àè b ‚àà t‚ÇÅ, f b) = (‚àè b ‚àà t‚ÇÇ \ s, f b) / ‚àè b ‚àà t‚ÇÅ \ s, f b := by
      rw [‚Üê Finset.prod_sdiff ht‚ÇÅ, ‚Üê Finset.prod_sdiff ht‚ÇÇ, mul_div_mul_right_eq_div]
    simp only [this]
    exact hde _ (h _ Finset.sdiff_disjoint) _ (h _ Finset.sdiff_disjoint)

@[to_additive]
theorem cauchySeq_finset_iff_tprod_vanishing :
    (CauchySeq fun s : Finset Œ≤ ‚Ü¶ ‚àè b ‚àà s, f b) ‚Üî
      ‚àÄ e ‚àà ùìù (1 : Œ±), ‚àÉ s : Finset Œ≤, ‚àÄ t : Set Œ≤, Disjoint t s ‚Üí (‚àè' b : t, f b) ‚àà e := by
  simp_rw [cauchySeq_finset_iff_prod_vanishing, Set.disjoint_left, disjoint_left]
  refine ‚ü®fun vanish e he ‚Ü¶ ?_, fun vanish e he ‚Ü¶ ?_‚ü©
  ¬∑ obtain ‚ü®o, ho, o_closed, oe‚ü© := exists_mem_nhds_isClosed_subset he
    obtain ‚ü®s, hs‚ü© := vanish o ho
    refine ‚ü®s, fun t hts ‚Ü¶ oe ?_‚ü©
    by_cases ht : Multipliable fun a : t ‚Ü¶ f a
    ¬∑ classical
      refine o_closed.mem_of_tendsto ht.hasProd (Eventually.of_forall fun t' ‚Ü¶ ?_)
      rw [‚Üê prod_subtype_map_embedding fun _ _ ‚Ü¶ by rfl]
      apply hs
      simp_rw [Finset.mem_map]
      rintro _ ‚ü®b, -, rfl‚ü©
      exact hts b.prop
    ¬∑ exact tprod_eq_one_of_not_multipliable ht ‚ñ∏ mem_of_mem_nhds ho
  ¬∑ obtain ‚ü®s, hs‚ü© := vanish _ he
    exact ‚ü®s, fun t hts ‚Ü¶ (t.tprod_subtype f).symm ‚ñ∏ hs _ hts‚ü©

variable [CompleteSpace Œ±]

@[to_additive]
theorem multipliable_iff_vanishing :
    Multipliable f ‚Üî
    ‚àÄ e ‚àà ùìù (1 : Œ±), ‚àÉ s : Finset Œ≤, ‚àÄ t, Disjoint t s ‚Üí (‚àè b ‚àà t, f b) ‚àà e := by
  rw [multipliable_iff_cauchySeq_finset, cauchySeq_finset_iff_prod_vanishing]

@[to_additive]
theorem multipliable_iff_tprod_vanishing : Multipliable f ‚Üî
    ‚àÄ e ‚àà ùìù (1 : Œ±), ‚àÉ s : Finset Œ≤, ‚àÄ t : Set Œ≤, Disjoint t s ‚Üí (‚àè' b : t, f b) ‚àà e := by
  rw [multipliable_iff_cauchySeq_finset, cauchySeq_finset_iff_tprod_vanishing]

-- TODO: generalize to monoid with a uniform continuous subtraction operator: `(a + b) - b = a`
@[to_additive]
theorem Multipliable.multipliable_of_eq_one_or_self (hf : Multipliable f)
    (h : ‚àÄ b, g b = 1 ‚à® g b = f b) : Multipliable g := by
  classical
  exact multipliable_iff_vanishing.2 fun e he ‚Ü¶
    let ‚ü®s, hs‚ü© := multipliable_iff_vanishing.1 hf e he
    ‚ü®s, fun t ht ‚Ü¶
      have eq : ‚àè b ‚àà t with g b = f b, f b = ‚àè b ‚àà t, g b :=
        calc
          ‚àè b ‚àà t with g b = f b, f b = ‚àè b ‚àà t with g b = f b, g b :=
            Finset.prod_congr rfl fun b hb ‚Ü¶ (Finset.mem_filter.1 hb).2.symm
          _ = ‚àè b ‚àà t, g b := by
           {refine Finset.prod_subset (Finset.filter_subset _ _) ?_
            intro b hbt hb
            simp only [Finset.mem_filter, and_iff_right hbt] at hb
            exact (h b).resolve_right hb}
      eq ‚ñ∏ hs _ <| Finset.disjoint_of_subset_left (Finset.filter_subset _ _) ht‚ü©

@[to_additive]
protected theorem Multipliable.mulIndicator (hf : Multipliable f) (s : Set Œ≤) :
    Multipliable (s.mulIndicator f) :=
  hf.multipliable_of_eq_one_or_self <| Set.mulIndicator_eq_one_or_self _ _

@[to_additive]
theorem Multipliable.comp_injective {i : Œ≥ ‚Üí Œ≤} (hf : Multipliable f) (hi : Injective i) :
    Multipliable (f ‚àò i) := by
  simpa only [Set.mulIndicator_range_comp] using
    (hi.multipliable_iff (fun x hx ‚Ü¶ Set.mulIndicator_of_notMem hx _)).2
    (hf.mulIndicator (Set.range i))

@[to_additive]
theorem Multipliable.subtype (hf : Multipliable f) (s : Set Œ≤) : Multipliable (f ‚àò (‚Üë) : s ‚Üí Œ±) :=
  hf.comp_injective Subtype.coe_injective

@[to_additive]
theorem multipliable_subtype_and_compl {s : Set Œ≤} :
    ((Multipliable fun x : s ‚Ü¶ f x) ‚àß Multipliable fun x : ‚Üës·∂ú ‚Ü¶ f x) ‚Üî Multipliable f :=
  ‚ü®and_imp.2 Multipliable.mul_compl, fun h ‚Ü¶ ‚ü®h.subtype s, h.subtype s·∂ú‚ü©‚ü©

@[to_additive]
protected theorem Multipliable.tprod_subtype_mul_tprod_subtype_compl [T2Space Œ±] {f : Œ≤ ‚Üí Œ±}
    (hf : Multipliable f) (s : Set Œ≤) : (‚àè' x : s, f x) * ‚àè' x : ‚Üës·∂ú, f x = ‚àè' x, f x :=
  ((hf.subtype s).hasProd.mul_compl (hf.subtype { x | x ‚àâ s }).hasProd).unique hf.hasProd

@[to_additive]
protected theorem Multipliable.prod_mul_tprod_subtype_compl [T2Space Œ±] {f : Œ≤ ‚Üí Œ±}
    (hf : Multipliable f) (s : Finset Œ≤) :
    (‚àè x ‚àà s, f x) * ‚àè' x : { x // x ‚àâ s }, f x = ‚àè' x, f x := by
  rw [‚Üê hf.tprod_subtype_mul_tprod_subtype_compl s]
  simp only [Finset.tprod_subtype', mul_right_inj]
  rfl

end IsUniformGroup

section IsTopologicalGroup

variable {G : Type*} [TopologicalSpace G] [CommGroup G] [IsTopologicalGroup G] {f : Œ± ‚Üí G}

@[to_additive]
theorem Multipliable.vanishing (hf : Multipliable f) ‚¶Ée : Set G‚¶Ñ (he : e ‚àà ùìù (1 : G)) :
    ‚àÉ s : Finset Œ±, ‚àÄ t, Disjoint t s ‚Üí (‚àè k ‚àà t, f k) ‚àà e := by
  classical
  letI : UniformSpace G := IsTopologicalGroup.toUniformSpace G
  have : IsUniformGroup G := isUniformGroup_of_commGroup
  exact cauchySeq_finset_iff_prod_vanishing.1 hf.hasProd.cauchySeq e he

@[to_additive]
theorem Multipliable.tprod_vanishing (hf : Multipliable f) ‚¶Ée : Set G‚¶Ñ (he : e ‚àà ùìù 1) :
    ‚àÉ s : Finset Œ±, ‚àÄ t : Set Œ±, Disjoint t s ‚Üí (‚àè' b : t, f b) ‚àà e := by
  classical
  letI : UniformSpace G := IsTopologicalGroup.toUniformSpace G
  have : IsUniformGroup G := isUniformGroup_of_commGroup
  exact cauchySeq_finset_iff_tprod_vanishing.1 hf.hasProd.cauchySeq e he

/-- The product over the complement of a finset tends to `1` when the finset grows to cover the
whole space. This does not need a multipliability assumption, as otherwise all such products are
one. -/
@[to_additive /-- The sum over the complement of a finset tends to `0` when the finset grows to
cover the whole space. This does not need a summability assumption, as otherwise all such sums are
zero. -/]
theorem tendsto_tprod_compl_atTop_one (f : Œ± ‚Üí G) :
    Tendsto (fun s : Finset Œ± ‚Ü¶ ‚àè' a : { x // x ‚àâ s }, f a) atTop (ùìù 1) := by
  classical
  by_cases H : Multipliable f
  ¬∑ intro e he
    obtain ‚ü®s, hs‚ü© := H.tprod_vanishing he
    rw [Filter.mem_map, mem_atTop_sets]
    exact ‚ü®s, fun t hts ‚Ü¶ hs _ <| Set.disjoint_left.mpr fun a ha has ‚Ü¶ ha (hts has)‚ü©
  ¬∑ refine tendsto_const_nhds.congr fun _ ‚Ü¶ (tprod_eq_one_of_not_multipliable ?_).symm
    rwa [Finset.multipliable_compl_iff]

/-- Product divergence test: if `f` is unconditionally multipliable, then `f x` tends to one along
`cofinite`. -/
@[to_additive /-- Series divergence test: if `f` is unconditionally summable, then `f x` tends to
zero along `cofinite`. -/]
theorem Multipliable.tendsto_cofinite_one (hf : Multipliable f) : Tendsto f cofinite (ùìù 1) := by
  intro e he
  rw [Filter.mem_map]
  rcases hf.vanishing he with ‚ü®s, hs‚ü©
  refine s.eventually_cofinite_notMem.mono fun x hx ‚Ü¶ ?_
  ¬∑ simpa using hs {x} (disjoint_singleton_left.2 hx)

@[to_additive]
theorem Multipliable.finite_mulSupport_of_discreteTopology
    {Œ± : Type*} [CommGroup Œ±] [TopologicalSpace Œ±] [DiscreteTopology Œ±]
    {Œ≤ : Type*} (f : Œ≤ ‚Üí Œ±) (h : Multipliable f) : Set.Finite f.mulSupport :=
  haveI : IsTopologicalGroup Œ± := ‚ü®‚ü©
  h.tendsto_cofinite_one (discreteTopology_iff_singleton_mem_nhds.mp ‚Äπ_‚Ä∫ 1)

@[to_additive]
theorem Multipliable.countable_mulSupport [FirstCountableTopology G] [T1Space G]
    (hf : Multipliable f) : f.mulSupport.Countable := by
  simpa only [ker_nhds] using hf.tendsto_cofinite_one.countable_compl_preimage_ker

@[to_additive]
theorem multipliable_const_iff [Infinite Œ≤] [T2Space G] (a : G) :
    Multipliable (fun _ : Œ≤ ‚Ü¶ a) ‚Üî a = 1 := by
  refine ‚ü®fun h ‚Ü¶ ?_, ?_‚ü©
  ¬∑ by_contra ha
    have : {a}·∂ú ‚àà ùìù 1 := compl_singleton_mem_nhds (Ne.symm ha)
    have : Finite Œ≤ := by
      simpa [‚Üê Set.finite_univ_iff] using h.tendsto_cofinite_one this
    exact not_finite Œ≤
  ¬∑ rintro rfl
    exact multipliable_one

@[to_additive (attr := simp)]
theorem tprod_const [T2Space G] (a : G) : ‚àè' _ : Œ≤, a = a ^ (Nat.card Œ≤) := by
  rcases finite_or_infinite Œ≤ with hŒ≤|hŒ≤
  ¬∑ letI : Fintype Œ≤ := Fintype.ofFinite Œ≤
    rw [tprod_eq_prod (s := univ) (fun x hx ‚Ü¶ (hx (mem_univ x)).elim)]
    simp only [prod_const, Nat.card_eq_fintype_card, Fintype.card]
  ¬∑ simp only [Nat.card_eq_zero_of_infinite, pow_zero]
    rcases eq_or_ne a 1 with rfl | ha
    ¬∑ simp
    ¬∑ apply tprod_eq_one_of_not_multipliable
      simpa [multipliable_const_iff] using ha

end IsTopologicalGroup

section CommGroupWithZero
variable {K : Type*} [CommGroupWithZero K] [TopologicalSpace K] [ContinuousMul K] {f g : Œ± ‚Üí K}
/-!
## Groups with a zero

These lemmas apply to a `CommGroupWithZero`; the most familiar case is when `K` is a field. These
are specific to the product setting and do not have a sensible additive analogue.
-/

open Finset in
lemma HasProd.congr_cofinite‚ÇÄ {c : K} (hc : HasProd f c) {s : Finset Œ±}
    (hs : ‚àÄ a ‚àà s, f a ‚â† 0) (hs' : ‚àÄ a ‚àâ s, f a = g a) :
    HasProd g (c * ((‚àè i ‚àà s, g i) / ‚àè i ‚àà s, f i)) := by
  classical
  refine (Tendsto.mul_const ((‚àè i ‚àà s, g i) / ‚àè i ‚àà s, f i) hc).congr' ?_
  filter_upwards [eventually_ge_atTop s] with t ht
  calc (‚àè i ‚àà t, f i) * ((‚àè i ‚àà s, g i) / ‚àè i ‚àà s, f i)
  _ = ((‚àè i ‚àà s, f i) * ‚àè i ‚àà t \ s, g i) * _ := by
    conv_lhs => rw [‚Üê union_sdiff_of_subset ht, prod_union disjoint_sdiff,
      prod_congr rfl fun i hi ‚Ü¶ hs' i (mem_sdiff.mp hi).2]
  _ = (‚àè i ‚àà s, g i) * ‚àè i ‚àà t \ s, g i := by
    rw [‚Üê mul_div_assoc, ‚Üê div_mul_eq_mul_div, ‚Üê div_mul_eq_mul_div, div_self, one_mul, mul_comm]
    exact prod_ne_zero_iff.mpr hs
  _ = ‚àè i ‚àà t, g i := by
    rw [‚Üê prod_union disjoint_sdiff, union_sdiff_of_subset ht]

protected lemma Multipliable.tsum_congr_cofinite‚ÇÄ [T2Space K] (hc : Multipliable f) {s : Finset Œ±}
    (hs : ‚àÄ a ‚àà s, f a ‚â† 0) (hs' : ‚àÄ a ‚àâ s, f a = g a) :
    ‚àè' i, g i = ((‚àè' i, f i) * ((‚àè i ‚àà s, g i) / ‚àè i ‚àà s, f i)) :=
  (hc.hasProd.congr_cofinite‚ÇÄ hs hs').tprod_eq

/--
See also `Multipliable.congr_cofinite`, which does not have a non-vanishing condition, but instead
requires the target to be a group under multiplication (and hence fails for infinite products in a
ring).
-/
lemma Multipliable.congr_cofinite‚ÇÄ (hf : Multipliable f) (hf' : ‚àÄ a, f a ‚â† 0)
    (hfg : ‚àÄ·∂† a in cofinite, f a = g a) :
    Multipliable g := by
  classical
  obtain ‚ü®c, hc‚ü© := hf
  obtain ‚ü®s, hs‚ü© : ‚àÉ s : Finset Œ±, ‚àÄ i ‚àâ s, f i = g i := ‚ü®hfg.toFinset, by simp‚ü©
  exact (hc.congr_cofinite‚ÇÄ (fun a _ ‚Ü¶ hf' a) hs).multipliable

end CommGroupWithZero
