/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl
-/

import Mathlib.Topology.Algebra.InfiniteSum.Group
import Mathlib.Logic.Encodable.Lattice

/-!
# Infinite sums over `‚Ñï` and `‚Ñ§`

This file contains lemmas about `HasSum`, `Summable`, and `tsum` applied to the important special
cases where the domain is `‚Ñï` or `‚Ñ§`. For instance, we prove the formula
`‚àë i in range k, f i + ‚àë' i, f (i + k) = ‚àë' i, f i`, in `sum_add_tsum_nat_add`, as well as several
results relating sums on `‚Ñï` and `‚Ñ§`.
-/

noncomputable section

open Filter Finset Function Encodable

open scoped BigOperators Topology

variable {M : Type*} [AddCommMonoid M] [TopologicalSpace M] {m m' : M}

variable {G : Type*} [AddCommGroup G] {g g' : G}
-- don't declare [TopologicalAddGroup G] here as some results require [UniformAddGroup G] instead

/-!
## Sums over `‚Ñï`
-/

section Nat

section Monoid

namespace HasSum

/-- If `f : ‚Ñï ‚Üí M` has sum `m`, then the partial sums `‚àë i in range n, f i` converge to `m`. -/
theorem tendsto_sum_nat {f : ‚Ñï ‚Üí M} (h : HasSum f m) :
    Tendsto (fun n ‚Ü¶ ‚àë i in range n, f i) atTop (ùìù m) :=
  h.comp tendsto_finset_range
#align has_sum.tendsto_sum_nat HasSum.tendsto_sum_nat

section ContinuousAdd

variable [ContinuousAdd M]

theorem sum_range_add {f : ‚Ñï ‚Üí M} {k : ‚Ñï} (h : HasSum (fun n ‚Ü¶ f (n + k)) m) :
    HasSum f ((‚àë i in range k, f i) + m) := by
  refine ((range k).hasSum f).add_compl ?_
  rwa [‚Üê (notMemRangeEquiv k).symm.hasSum_iff]

theorem zero_add {f : ‚Ñï ‚Üí M} (h : HasSum (fun n ‚Ü¶ f (n + 1)) m) :
    HasSum f (f 0 + m) := by
  simpa only [sum_range_one] using h.sum_range_add

theorem even_add_odd {f : ‚Ñï ‚Üí M} (he : HasSum (fun k ‚Ü¶ f (2 * k)) m)
    (ho : HasSum (fun k ‚Ü¶ f (2 * k + 1)) m') : HasSum f (m + m') := by
  have := mul_right_injective‚ÇÄ (two_ne_zero' ‚Ñï)
  replace ho := ((add_left_injective 1).comp this).hasSum_range_iff.2 ho
  refine (this.hasSum_range_iff.2 he).add_isCompl ?_ ho
  simpa [(¬∑ ‚àò ¬∑)] using Nat.isCompl_even_odd
#align has_sum.even_add_odd HasSum.even_add_odd

end ContinuousAdd

end HasSum

namespace Summable

theorem hasSum_iff_tendsto_nat [T2Space M] {f : ‚Ñï ‚Üí M} (hf : Summable f) :
    HasSum f m ‚Üî Tendsto (fun n : ‚Ñï ‚Ü¶ ‚àë i in range n, f i) atTop (ùìù m) := by
  refine ‚ü®fun h ‚Ü¶ h.tendsto_sum_nat, fun h ‚Ü¶ ?_‚ü©
  rw [tendsto_nhds_unique h hf.hasSum.tendsto_sum_nat]
  exact hf.hasSum
#align summable.has_sum_iff_tendsto_nat Summable.hasSum_iff_tendsto_nat

section ContinuousAdd

variable [ContinuousAdd M]

theorem comp_nat_add {f : ‚Ñï ‚Üí M} {k : ‚Ñï} (h : Summable fun n ‚Ü¶ f (n + k)) : Summable f :=
  h.hasSum.sum_range_add.summable

theorem even_add_odd {f : ‚Ñï ‚Üí M} (he : Summable fun k ‚Ü¶ f (2 * k))
    (ho : Summable fun k ‚Ü¶ f (2 * k + 1)) : Summable f :=
  (he.hasSum.even_add_odd ho.hasSum).summable

end ContinuousAdd

end Summable

section tsum

variable [T2Space M] {Œ± Œ≤ Œ≥  : Type*}

section Encodable

variable [Encodable Œ≤]

/-- You can compute a sum over an encodable type by summing over the natural numbers and
  taking a supremum. This is useful for outer measures. -/
theorem tsum_iSup_decode‚ÇÇ [CompleteLattice Œ±] (m : Œ± ‚Üí M) (m0 : m ‚ä• = 0) (s : Œ≤ ‚Üí Œ±) :
    ‚àë' i : ‚Ñï, m (‚®Ü b ‚àà decode‚ÇÇ Œ≤ i, s b) = ‚àë' b : Œ≤, m (s b) := by
  rw [‚Üê tsum_extend_zero (@encode_injective Œ≤ _)]
  refine tsum_congr fun n ‚Ü¶ ?_
  rcases em (n ‚àà Set.range (encode : Œ≤ ‚Üí ‚Ñï)) with ‚ü®a, rfl‚ü© | hn
  ¬∑ simp [encode_injective.extend_apply]
  ¬∑ rw [extend_apply' _ _ _ hn]
    rw [‚Üê decode‚ÇÇ_ne_none_iff, ne_eq, not_not] at hn
    simp [hn, m0]
#align tsum_supr_decode‚ÇÇ tsum_iSup_decode‚ÇÇ

/-- `tsum_iSup_decode‚ÇÇ` specialized to the complete lattice of sets. -/
theorem tsum_iUnion_decode‚ÇÇ (m : Set Œ± ‚Üí M) (m0 : m ‚àÖ = 0) (s : Œ≤ ‚Üí Set Œ±) :
    ‚àë' i, m (‚ãÉ b ‚àà decode‚ÇÇ Œ≤ i, s b) = ‚àë' b, m (s b) :=
  tsum_iSup_decode‚ÇÇ m m0 s
#align tsum_Union_decode‚ÇÇ tsum_iUnion_decode‚ÇÇ

end Encodable

/-! Some properties about measure-like functions. These could also be functions defined on complete
  sublattices of sets, with the property that they are countably sub-additive.
  `R` will probably be instantiated with `(‚â§)` in all applications.
-/
section Countable

variable [Countable Œ≤]

/-- If a function is countably sub-additive then it is sub-additive on countable types -/
theorem rel_iSup_tsum [CompleteLattice Œ±] (m : Œ± ‚Üí M) (m0 : m ‚ä• = 0) (R : M ‚Üí M ‚Üí Prop)
    (m_iSup : ‚àÄ s : ‚Ñï ‚Üí Œ±, R (m (‚®Ü i, s i)) (‚àë' i, m (s i))) (s : Œ≤ ‚Üí Œ±) :
    R (m (‚®Ü b : Œ≤, s b)) (‚àë' b : Œ≤, m (s b)) := by
  cases nonempty_encodable Œ≤
  rw [‚Üê iSup_decode‚ÇÇ, ‚Üê tsum_iSup_decode‚ÇÇ _ m0 s]
  exact m_iSup _
#align rel_supr_tsum rel_iSup_tsum

/-- If a function is countably sub-additive then it is sub-additive on finite sets -/
theorem rel_iSup_sum [CompleteLattice Œ±] (m : Œ± ‚Üí M) (m0 : m ‚ä• = 0) (R : M ‚Üí M ‚Üí Prop)
    (m_iSup : ‚àÄ s : ‚Ñï ‚Üí Œ±, R (m (‚®Ü i, s i)) (‚àë' i, m (s i))) (s : Œ≥ ‚Üí Œ±) (t : Finset Œ≥) :
    R (m (‚®Ü d ‚àà t, s d)) (‚àë d in t, m (s d)) := by
  rw [iSup_subtype', ‚Üê Finset.tsum_subtype]
  exact rel_iSup_tsum m m0 R m_iSup _
#align rel_supr_sum rel_iSup_sum

/-- If a function is countably sub-additive then it is binary sub-additive -/
theorem rel_sup_add [CompleteLattice Œ±] (m : Œ± ‚Üí M) (m0 : m ‚ä• = 0) (R : M ‚Üí M ‚Üí Prop)
    (m_iSup : ‚àÄ s : ‚Ñï ‚Üí Œ±, R (m (‚®Ü i, s i)) (‚àë' i, m (s i))) (s‚ÇÅ s‚ÇÇ : Œ±) :
    R (m (s‚ÇÅ ‚äî s‚ÇÇ)) (m s‚ÇÅ + m s‚ÇÇ) := by
  convert rel_iSup_tsum m m0 R m_iSup fun b ‚Ü¶ cond b s‚ÇÅ s‚ÇÇ
  ¬∑ simp only [iSup_bool_eq, cond]
  ¬∑ rw [tsum_fintype, Fintype.sum_bool, cond, cond]
#align rel_sup_add rel_sup_add

end Countable

section ContinuousAdd

variable [ContinuousAdd M]

theorem sum_add_tsum_nat_add'
    {f : ‚Ñï ‚Üí M} {k : ‚Ñï} (h : Summable (fun n ‚Ü¶ f (n + k))) :
    ((‚àë i in range k, f i) + ‚àë' i, f (i + k)) = ‚àë' i, f i :=
  h.hasSum.sum_range_add.tsum_eq.symm

theorem tsum_eq_zero_add'
    {f : ‚Ñï ‚Üí M} (hf : Summable (fun n ‚Ü¶ f (n + 1))) :
    ‚àë' b, f b = f 0 + ‚àë' b, f (b + 1) := by
  simpa only [sum_range_one] using (sum_add_tsum_nat_add' hf).symm

theorem tsum_even_add_odd {f : ‚Ñï ‚Üí M} (he : Summable fun k ‚Ü¶ f (2 * k))
    (ho : Summable fun k ‚Ü¶ f (2 * k + 1)) :
    ‚àë' k, f (2 * k) + ‚àë' k, f (2 * k + 1) = ‚àë' k, f k :=
  (he.hasSum.even_add_odd ho.hasSum).tsum_eq.symm
#align tsum_even_add_odd tsum_even_add_odd

end ContinuousAdd

end tsum

end Monoid

section TopologicalGroup

variable [TopologicalSpace G] [TopologicalAddGroup G]

theorem hasSum_nat_add_iff {f : ‚Ñï ‚Üí G} (k : ‚Ñï) :
    HasSum (fun n ‚Ü¶ f (n + k)) g ‚Üî HasSum f (g + ‚àë i in range k, f i) := by
  refine Iff.trans ?_ (range k).hasSum_compl_iff
  rw [‚Üê (notMemRangeEquiv k).symm.hasSum_iff]
  rfl
#align has_sum_nat_add_iff hasSum_nat_add_iff

theorem summable_nat_add_iff {f : ‚Ñï ‚Üí G} (k : ‚Ñï) : (Summable fun n ‚Ü¶ f (n + k)) ‚Üî Summable f :=
  Iff.symm <|
    (Equiv.addRight (‚àë i in range k, f i)).surjective.summable_iff_of_hasSum_iff
      (hasSum_nat_add_iff k).symm
#align summable_nat_add_iff summable_nat_add_iff

theorem hasSum_nat_add_iff' {f : ‚Ñï ‚Üí G} (k : ‚Ñï) :
    HasSum (fun n ‚Ü¶ f (n + k)) (g - ‚àë i in range k, f i) ‚Üî HasSum f g := by
  simp [hasSum_nat_add_iff]
#align has_sum_nat_add_iff' hasSum_nat_add_iff'

theorem sum_add_tsum_nat_add [T2Space G] {f : ‚Ñï ‚Üí G} (k : ‚Ñï) (h : Summable f) :
    ((‚àë i in range k, f i) + ‚àë' i, f (i + k)) = ‚àë' i, f i :=
  sum_add_tsum_nat_add' <| (summable_nat_add_iff k).2 h
#align sum_add_tsum_nat_add sum_add_tsum_nat_add

theorem tsum_eq_zero_add [T2Space G] {f : ‚Ñï ‚Üí G} (hf : Summable f) :
    ‚àë' b, f b = f 0 + ‚àë' b, f (b + 1) :=
  tsum_eq_zero_add' <| (summable_nat_add_iff 1).2 hf
#align tsum_eq_zero_add tsum_eq_zero_add

/-- For `f : ‚Ñï ‚Üí G`, then `‚àë' k, f (k + i)` tends to zero. This does not require a summability
assumption on `f`, as otherwise all sums are zero. -/
theorem tendsto_sum_nat_add [T2Space G] (f : ‚Ñï ‚Üí G) :
    Tendsto (fun i ‚Ü¶ ‚àë' k, f (k + i)) atTop (ùìù 0) := by
  by_cases hf : Summable f
  ¬∑ have h‚ÇÄ : (fun i ‚Ü¶ ‚àë' i, f i - ‚àë j in range i, f j) = fun i ‚Ü¶ ‚àë' k : ‚Ñï, f (k + i) := by
      ext1 i
      rw [sub_eq_iff_eq_add, add_comm, sum_add_tsum_nat_add i hf]
    have h‚ÇÅ : Tendsto (fun _ : ‚Ñï ‚Ü¶ ‚àë' i, f i) atTop (ùìù (‚àë' i, f i)) := tendsto_const_nhds
    simpa only [h‚ÇÄ, sub_self] using Tendsto.sub h‚ÇÅ hf.hasSum.tendsto_sum_nat
  ¬∑ refine tendsto_const_nhds.congr fun n ‚Ü¶ (tsum_eq_zero_of_not_summable ?_).symm
    rwa [summable_nat_add_iff n]
#align tendsto_sum_nat_add tendsto_sum_nat_add

end TopologicalGroup

section UniformGroup

variable [UniformSpace G] [UniformAddGroup G]

theorem cauchySeq_finset_iff_nat_tsum_vanishing {f : ‚Ñï ‚Üí G} :
    (CauchySeq fun s : Finset ‚Ñï ‚Ü¶ ‚àë n in s, f n) ‚Üî
      ‚àÄ e ‚àà ùìù (0 : G), ‚àÉ N : ‚Ñï, ‚àÄ t ‚äÜ {n | N ‚â§ n}, (‚àë' n : t, f n) ‚àà e := by
  refine cauchySeq_finset_iff_tsum_vanishing.trans ‚ü®fun vanish e he ‚Ü¶ ?_, fun vanish e he ‚Ü¶ ?_‚ü©
  ¬∑ obtain ‚ü®s, hs‚ü© := vanish e he
    refine ‚ü®if h : s.Nonempty then s.max' h + 1 else 0, fun t ht ‚Ü¶ hs _ <| Set.disjoint_left.mpr ?_‚ü©
    split_ifs at ht with h
    ¬∑ exact fun m hmt hms ‚Ü¶ (s.le_max' _ hms).not_lt (Nat.succ_le_iff.mp <| ht hmt)
    ¬∑ exact fun _ _ hs ‚Ü¶ h ‚ü®_, hs‚ü©
  ¬∑ obtain ‚ü®N, hN‚ü© := vanish e he
    exact ‚ü®range N, fun t ht ‚Ü¶ hN _ fun n hnt ‚Ü¶
      le_of_not_lt fun h ‚Ü¶ Set.disjoint_left.mp ht hnt (mem_range.mpr h)‚ü©

variable [CompleteSpace G]

theorem summable_iff_nat_tsum_vanishing {f : ‚Ñï ‚Üí G} : Summable f ‚Üî
    ‚àÄ e ‚àà ùìù 0, ‚àÉ N : ‚Ñï, ‚àÄ t ‚äÜ {n | N ‚â§ n}, (‚àë' n : t, f n) ‚àà e := by
  rw [summable_iff_cauchySeq_finset, cauchySeq_finset_iff_nat_tsum_vanishing]

end UniformGroup

section TopologicalGroup

variable [TopologicalSpace G] [TopologicalAddGroup G]

theorem Summable.nat_tsum_vanishing {f : ‚Ñï ‚Üí G} (hf : Summable f) ‚¶Ée : Set G‚¶Ñ (he : e ‚àà ùìù 0) :
    ‚àÉ N : ‚Ñï, ‚àÄ t ‚äÜ {n | N ‚â§ n}, (‚àë' n : t, f n) ‚àà e :=
  letI : UniformSpace G := TopologicalAddGroup.toUniformSpace G
  have : UniformAddGroup G := comm_topologicalAddGroup_is_uniform
  cauchySeq_finset_iff_nat_tsum_vanishing.1 hf.hasSum.cauchySeq e he

theorem Summable.tendsto_atTop_zero {f : ‚Ñï ‚Üí G} (hf : Summable f) : Tendsto f atTop (ùìù 0) := by
  rw [‚Üê Nat.cofinite_eq_atTop]
  exact hf.tendsto_cofinite_zero
#align summable.tendsto_at_top_zero Summable.tendsto_atTop_zero

end TopologicalGroup

end Nat

/-!
## Sums over `‚Ñ§`

In this section we prove a variety of lemmas relating sums over `‚Ñï` to sums over `‚Ñ§`.
-/

section Int

section Monoid

lemma HasSum.nat_add_neg_add_one {f : ‚Ñ§ ‚Üí M} (hf : HasSum f m) :
    HasSum (fun n : ‚Ñï ‚Ü¶ f n + f (-(n + 1))) m := by
  change HasSum (fun n : ‚Ñï ‚Ü¶ f n + f (Int.negSucc n)) m
  have : Injective Int.negSucc := @Int.negSucc.inj
  refine hf.hasSum_of_sum_eq fun u ‚Ü¶ ?_
  refine ‚ü®u.preimage _ (Nat.cast_injective.injOn _) ‚à™ u.preimage _ (this.injOn _),
      fun v' hv' ‚Ü¶ ‚ü®v'.image Nat.cast ‚à™ v'.image Int.negSucc, fun x hx ‚Ü¶ ?_, ?_‚ü©‚ü©
  ¬∑ simp only [mem_union, mem_image]
    cases x
    ¬∑ exact Or.inl ‚ü®_, hv' (by simpa using Or.inl hx), rfl‚ü©
    ¬∑ exact Or.inr ‚ü®_, hv' (by simpa using Or.inr hx), rfl‚ü©
  ¬∑ rw [sum_union, sum_image (Nat.cast_injective.injOn _), sum_image (this.injOn _),
      sum_add_distrib]
    simp only [disjoint_iff_ne, mem_image, ne_eq, forall_exists_index, and_imp,
      forall_apply_eq_imp_iff‚ÇÇ, not_false_eq_true, implies_true, forall_const]

lemma Summable.nat_add_neg_add_one {f : ‚Ñ§ ‚Üí M} (hf : Summable f) :
    Summable (fun n : ‚Ñï ‚Ü¶ f n + f (-(n + 1))) :=
  hf.hasSum.nat_add_neg_add_one.summable

lemma tsum_nat_add_neg_add_one [T2Space M] {f : ‚Ñ§ ‚Üí M} (hf : Summable f) :
    ‚àë' (n : ‚Ñï), (f n + f (-(n + 1))) = ‚àë' (n : ‚Ñ§), f n :=
  hf.hasSum.nat_add_neg_add_one.tsum_eq

section ContinuousAdd

variable [ContinuousAdd M]

lemma HasSum.of_nat_of_neg_add_one {f : ‚Ñ§ ‚Üí M}
    (hf‚ÇÅ : HasSum (fun n : ‚Ñï ‚Ü¶ f n) m) (hf‚ÇÇ : HasSum (fun n : ‚Ñï ‚Ü¶ f (-(n + 1))) m') :
    HasSum f (m + m') := by
  have hi‚ÇÅ : Injective ((‚Üë) : ‚Ñï ‚Üí ‚Ñ§) := @Int.ofNat.inj
  have hi‚ÇÇ : Injective Int.negSucc := @Int.negSucc.inj
  have : IsCompl (Set.range ((‚Üë) : ‚Ñï ‚Üí ‚Ñ§)) (Set.range Int.negSucc) := by
    constructor
    ¬∑ rw [disjoint_iff_inf_le]
      rintro _ ‚ü®‚ü®i, rfl‚ü©, ‚ü®j, ‚ü®‚ü©‚ü©‚ü©
    ¬∑ rw [codisjoint_iff_le_sup]
      rintro (i | j) <;> simp
  exact (hi‚ÇÅ.hasSum_range_iff.mpr hf‚ÇÅ).add_isCompl this (hi‚ÇÇ.hasSum_range_iff.mpr hf‚ÇÇ)
#align has_sum.nonneg_add_neg HasSum.of_nat_of_neg_add_one

-- deprecated 2024-03-04
@[deprecated] alias HasSum.nonneg_add_neg := HasSum.of_nat_of_neg_add_one

lemma Summable.of_nat_of_neg_add_one {f : ‚Ñ§ ‚Üí M}
    (hf‚ÇÅ : Summable fun n : ‚Ñï ‚Ü¶ f n)  (hf‚ÇÇ : Summable fun n : ‚Ñï ‚Ü¶ f (-(n + 1))) :
    Summable f :=
  (hf‚ÇÅ.hasSum.of_nat_of_neg_add_one hf‚ÇÇ.hasSum).summable

lemma tsum_of_nat_of_neg_add_one [T2Space M] {f : ‚Ñ§ ‚Üí M}
    (hf‚ÇÅ : Summable fun n : ‚Ñï ‚Ü¶ f n) (hf‚ÇÇ : Summable fun n : ‚Ñï ‚Ü¶ f (-(n + 1))) :
    ‚àë' n : ‚Ñ§, f n = ‚àë' n : ‚Ñï, f n + ‚àë' n : ‚Ñï, f (-(n + 1)) :=
  (hf‚ÇÅ.hasSum.of_nat_of_neg_add_one hf‚ÇÇ.hasSum).tsum_eq

/-- If `f‚ÇÄ, f‚ÇÅ, f‚ÇÇ, ...` and `g‚ÇÄ, g‚ÇÅ, g‚ÇÇ, ...` have sums `a`, `b` respectively, then the `‚Ñ§`-indexed
sequence: `..., g‚ÇÇ, g‚ÇÅ, g‚ÇÄ, f‚ÇÄ, f‚ÇÅ, f‚ÇÇ, ...` (with `f‚ÇÄ` at the `0`-th position) has sum `a + b`. -/
lemma HasSum.int_rec {f g : ‚Ñï ‚Üí M} (hf : HasSum f m) (hg : HasSum g m') :
    HasSum (Int.rec f g) (m + m') :=
  HasSum.of_nat_of_neg_add_one hf hg
#align has_sum.int_rec HasSum.int_rec

/-- If `f‚ÇÄ, f‚ÇÅ, f‚ÇÇ, ...` and `g‚ÇÄ, g‚ÇÅ, g‚ÇÇ, ...` are both summable then so is the `‚Ñ§`-indexed
sequence: `..., g‚ÇÇ, g‚ÇÅ, g‚ÇÄ, f‚ÇÄ, f‚ÇÅ, f‚ÇÇ, ...` (with `f‚ÇÄ` at the `0`-th position). -/
lemma Summable.int_rec {f g : ‚Ñï ‚Üí M} (hf : Summable f) (hg : Summable g) : Summable (Int.rec f g) :=
  .of_nat_of_neg_add_one hf hg

/-- If `f‚ÇÄ, f‚ÇÅ, f‚ÇÇ, ...` and `g‚ÇÄ, g‚ÇÅ, g‚ÇÇ, ...` are both summable, then the sum of the `‚Ñ§`-indexed
sequence: `..., g‚ÇÇ, g‚ÇÅ, g‚ÇÄ, f‚ÇÄ, f‚ÇÅ, f‚ÇÇ, ...` (with `f‚ÇÄ` at the `0`-th position) is
`‚àë' n, f n + ‚àë' n, g n`. -/
lemma tsum_int_rec [T2Space M] {f g : ‚Ñï ‚Üí M} (hf : Summable f) (hg : Summable g) :
    ‚àë' n : ‚Ñ§, Int.rec f g n = ‚àë' n : ‚Ñï, f n + ‚àë' n : ‚Ñï, g n :=
  (hf.hasSum.int_rec hg.hasSum).tsum_eq

theorem HasSum.nat_add_neg {f : ‚Ñ§ ‚Üí M} (hf : HasSum f m) :
    HasSum (fun n : ‚Ñï ‚Ü¶ f n + f (-n)) (m + f 0) := by
  -- Note this is much easier to prove if you assume more about the target space, but we have to
  -- work hard to prove it under the very minimal assumptions here.
  apply (hf.add (hasSum_ite_eq (0 : ‚Ñ§) (f 0))).hasSum_of_sum_eq fun u ‚Ü¶ ?_
  refine ‚ü®u.image Int.natAbs, fun v' hv' ‚Ü¶ ?_‚ü©
  let u1 := v'.image fun x : ‚Ñï ‚Ü¶ (x : ‚Ñ§)
  let u2 := v'.image fun x : ‚Ñï ‚Ü¶ -(x : ‚Ñ§)
  have A : u ‚äÜ u1 ‚à™ u2 := by
    intro x hx
    simp only [u1, u2, mem_union, mem_image, exists_prop]
    rcases le_total 0 x with (h'x | h'x)
    ¬∑ refine Or.inl ‚ü®_, hv' <| mem_image.mpr ‚ü®x, hx, rfl‚ü©, ?_‚ü©
      simp only [Int.coe_natAbs, abs_eq_self, h'x]
    ¬∑ refine Or.inr ‚ü®_, hv' <| mem_image.mpr ‚ü®x, hx, rfl‚ü©, ?_‚ü©
      simp only [abs_of_nonpos h'x, Int.coe_natAbs, neg_neg]
  exact ‚ü®_, A, calc
    (‚àë x in u1 ‚à™ u2, (f x + if x = 0 then f 0 else 0)) =
        (‚àë x in u1 ‚à™ u2, f x) + ‚àë x in u1 ‚à© u2, f x := by
      rw [sum_add_distrib]
      congr 1
      refine (sum_subset_zero_on_sdiff inter_subset_union ?_ ?_).symm
      ¬∑ intro x hx
        suffices x ‚â† 0 by simp only [this, if_false]
        rintro rfl
        simp only [mem_sdiff, mem_union, mem_image, Nat.cast_eq_zero, exists_eq_right, neg_eq_zero,
          or_self, mem_inter, and_self, and_not_self, u1, u2] at hx
      ¬∑ intro x hx
        simp only [u1, u2, mem_inter, mem_image, exists_prop] at hx
        suffices x = 0 by simp only [this, eq_self_iff_true, if_true]
        apply le_antisymm
        ¬∑ rcases hx.2 with ‚ü®a, _, rfl‚ü©
          simp only [Right.neg_nonpos_iff, Nat.cast_nonneg]
        ¬∑ rcases hx.1 with ‚ü®a, _, rfl‚ü©
          simp only [Nat.cast_nonneg]
    _ = (‚àë x in u1, f x) + ‚àë x in u2, f x := sum_union_inter
    _ = (‚àë b in v', f b) + ‚àë b in v', f (-b) := by
      simp only [u1, u2, Nat.cast_inj, imp_self, implies_true, forall_const, sum_image, neg_inj]
    _ = ‚àë b in v', (f b + f (-b)) := sum_add_distrib.symm‚ü©
#align has_sum.sum_nat_of_sum_int HasSum.nat_add_neg

-- deprecated 2024-03-04
@[deprecated HasSum.nat_add_neg] alias HasSum.sum_nat_of_sum_int :=
  HasSum.nat_add_neg

theorem Summable.nat_add_neg {f : ‚Ñ§ ‚Üí M} (hf : Summable f) :
    Summable fun n : ‚Ñï ‚Ü¶ f n + f (-n) :=
  hf.hasSum.nat_add_neg.summable

lemma tsum_nat_add_neg [T2Space M] {f : ‚Ñ§ ‚Üí M} (hf : Summable f) :
    ‚àë' n : ‚Ñï, (f n + f (-n)) = (‚àë' n : ‚Ñ§, f n) + f 0 :=
  hf.hasSum.nat_add_neg.tsum_eq

theorem HasSum.of_add_one_of_neg_add_one {f : ‚Ñ§ ‚Üí M}
    (hf‚ÇÅ : HasSum (fun n : ‚Ñï ‚Ü¶ f (n + 1)) m) (hf‚ÇÇ : HasSum (fun n : ‚Ñï ‚Ü¶ f (-(n + 1))) m') :
    HasSum f (m + f 0 + m') :=
  HasSum.of_nat_of_neg_add_one (add_comm _ m ‚ñ∏ HasSum.zero_add hf‚ÇÅ) hf‚ÇÇ
#align has_sum.pos_add_zero_add_neg HasSum.of_add_one_of_neg_add_one

-- deprecated 2024-03-04
@[deprecated HasSum.of_add_one_of_neg_add_one] alias HasSum.pos_add_zero_add_neg :=
  HasSum.of_add_one_of_neg_add_one

lemma Summable.of_add_one_of_neg_add_one {f : ‚Ñ§ ‚Üí M}
    (hf‚ÇÅ : Summable fun n : ‚Ñï ‚Ü¶ f (n + 1)) (hf‚ÇÇ : Summable fun n : ‚Ñï ‚Ü¶ f (-(n + 1))) :
    Summable f :=
  (hf‚ÇÅ.hasSum.of_add_one_of_neg_add_one hf‚ÇÇ.hasSum).summable

lemma tsum_of_add_one_of_neg_add_one [T2Space M] {f : ‚Ñ§ ‚Üí M}
    (hf‚ÇÅ : Summable fun n : ‚Ñï ‚Ü¶ f (n + 1)) (hf‚ÇÇ : Summable fun n : ‚Ñï ‚Ü¶ f (-(n + 1))) :
    ‚àë' n : ‚Ñ§, f n = ‚àë' n : ‚Ñï, f (n + 1) + f 0 + ‚àë' n : ‚Ñï, f (-(n + 1)) :=
  (hf‚ÇÅ.hasSum.of_add_one_of_neg_add_one hf‚ÇÇ.hasSum).tsum_eq

end ContinuousAdd

end Monoid

section TopologicalGroup

variable [TopologicalSpace G] [TopologicalAddGroup G]

lemma HasSum.of_nat_of_neg {f : ‚Ñ§ ‚Üí G} (hf‚ÇÅ : HasSum (fun n : ‚Ñï ‚Ü¶ f n) g)
    (hf‚ÇÇ : HasSum (fun n : ‚Ñï ‚Ü¶ f (-n)) g') : HasSum f (g + g' - f 0) := by
  refine add_sub_assoc' g .. ‚ñ∏ hf‚ÇÅ.nonneg_add_neg (m' := g' - f 0) ?_
  rwa [‚Üê hasSum_nat_add_iff' 1, sum_range_one, Nat.cast_zero, neg_zero] at hf‚ÇÇ

lemma Summable.of_nat_of_neg {f : ‚Ñ§ ‚Üí G} (hf‚ÇÅ : Summable fun n : ‚Ñï ‚Ü¶ f n)
    (hf‚ÇÇ : Summable fun n : ‚Ñï ‚Ü¶ f (-n)) : Summable f :=
  (hf‚ÇÅ.hasSum.of_nat_of_neg hf‚ÇÇ.hasSum).summable
#align summable_int_of_summable_nat Summable.of_nat_of_neg

-- deprecated 2024-03-04
@[deprecated Summable.of_nat_of_neg] alias summable_int_of_summable_nat :=
  Summable.of_nat_of_neg

lemma tsum_of_nat_of_neg [T2Space G] {f : ‚Ñ§ ‚Üí G}
    (hf‚ÇÅ : Summable fun n : ‚Ñï ‚Ü¶ f n) (hf‚ÇÇ : Summable fun n : ‚Ñï ‚Ü¶ f (-n)) :
    ‚àë' n : ‚Ñ§, f n = ‚àë' n : ‚Ñï, f n + ‚àë' n : ‚Ñï, f (-n) - f 0 :=
  (hf‚ÇÅ.hasSum.of_nat_of_neg hf‚ÇÇ.hasSum).tsum_eq

end TopologicalGroup

section UniformGroup -- results which depend on completeness

variable [UniformSpace G] [UniformAddGroup G] [CompleteSpace G]

/-- "iff" version of `Summable.of_nat_of_neg_add_one`. -/
lemma summable_int_iff_summable_nat_and_neg_add_one {f : ‚Ñ§ ‚Üí G} :
    Summable f ‚Üî (Summable fun n : ‚Ñï ‚Ü¶ f n) ‚àß (Summable fun n : ‚Ñï ‚Ü¶ f (-(n + 1))) := by
  refine ‚ü®fun p ‚Ü¶ ‚ü®?_, ?_‚ü©, fun ‚ü®hf‚ÇÅ, hf‚ÇÇ‚ü© ‚Ü¶ Summable.of_nat_of_neg_add_one hf‚ÇÅ hf‚ÇÇ‚ü© <;>
  apply p.comp_injective
  exacts [Nat.cast_injective, @Int.negSucc.inj]

/-- "iff" version of `Summable.of_natCast_neg_natCast`. -/
lemma summable_int_iff_summable_nat_and_neg {f : ‚Ñ§ ‚Üí G} :
    Summable f ‚Üî (Summable fun n : ‚Ñï ‚Ü¶ f n) ‚àß (Summable fun n : ‚Ñï ‚Ü¶ f (-n)) := by
  refine ‚ü®fun p ‚Ü¶ ‚ü®?_, ?_‚ü©, fun ‚ü®hf‚ÇÅ, hf‚ÇÇ‚ü© ‚Ü¶ Summable.of_nat_of_neg hf‚ÇÅ hf‚ÇÇ‚ü© <;>
  apply p.comp_injective
  exacts [Nat.cast_injective, neg_injective.comp Nat.cast_injective]

end UniformGroup

end Int
