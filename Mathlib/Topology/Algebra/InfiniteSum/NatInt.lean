/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl
-/

import Mathlib.Topology.Algebra.InfiniteSum.Group
import Mathlib.Logic.Encodable.Lattice

/-!
# Infinite sums over `‚Ñï` and `‚Ñ§`

This file contains lemmas about `HasSum`, `Summable`, and `tsum` applied to the important special
cases where the domain is `‚Ñï` or `‚Ñ§`. For instance, we prove the formula
`‚àë i in range k, f i + ‚àë' i, f (i + k) = ‚àë' i, f i`, in `sum_add_tsum_nat_add`, as well as several
results relating sums on `‚Ñï` and `‚Ñ§`.
-/

noncomputable section

open Filter Finset Function Encodable

open scoped BigOperators Topology

variable {Œ± Œ≤ Œ≥ Œ¥ : Type*}

/-!
## Sums over `‚Ñï`
-/

section Nat

section Monoid

variable [AddCommMonoid Œ±] [TopologicalSpace Œ±]

namespace HasSum

variable {a b : Œ±}

/-- If `f : ‚Ñï ‚Üí Œ±` has sum `a`, then the partial sums `‚àë_{i=0}^{n-1} f i` converge to `a`. -/
theorem tendsto_sum_nat {f : ‚Ñï ‚Üí Œ±} (h : HasSum f a) :
    Tendsto (fun n : ‚Ñï ‚Ü¶ ‚àë i in range n, f i) atTop (ùìù a) :=
  h.comp tendsto_finset_range
#align has_sum.tendsto_sum_nat HasSum.tendsto_sum_nat

section ContinuousAdd

variable [ContinuousAdd Œ±]

theorem sum_range_add {f : ‚Ñï ‚Üí Œ±}
    {k : ‚Ñï} {a : Œ±} (h : HasSum (fun n ‚Ü¶ f (n + k)) a) : HasSum f ((‚àë i in range k, f i) + a) := by
  refine ((range k).hasSum f).add_compl ?_
  rwa [‚Üê (notMemRangeEquiv k).symm.hasSum_iff]

theorem zero_add {f : ‚Ñï ‚Üí Œ±} {a : Œ±} (h : HasSum (fun n ‚Ü¶ f (n + 1)) a) :
    HasSum f (f 0 + a) := by
  simpa only [sum_range_one] using h.sum_range_add

theorem even_add_odd {f : ‚Ñï ‚Üí Œ±} (he : HasSum (fun k ‚Ü¶ f (2 * k)) a)
    (ho : HasSum (fun k ‚Ü¶ f (2 * k + 1)) b) : HasSum f (a + b) := by
  have := mul_right_injective‚ÇÄ (two_ne_zero' ‚Ñï)
  replace ho := ((add_left_injective 1).comp this).hasSum_range_iff.2 ho
  refine' (this.hasSum_range_iff.2 he).add_isCompl _ ho
  simpa [(¬∑ ‚àò ¬∑)] using Nat.isCompl_even_odd
#align has_sum.even_add_odd HasSum.even_add_odd

end ContinuousAdd

end HasSum

namespace Summable

theorem hasSum_iff_tendsto_nat [T2Space Œ±] {f : ‚Ñï ‚Üí Œ±} {a : Œ±} (hf : Summable f) :
    HasSum f a ‚Üî Tendsto (fun n : ‚Ñï ‚Ü¶ ‚àë i in range n, f i) atTop (ùìù a) := by
  refine ‚ü®fun h ‚Ü¶ h.tendsto_sum_nat, fun h ‚Ü¶ ?_‚ü©
  rw [tendsto_nhds_unique h hf.hasSum.tendsto_sum_nat]
  exact hf.hasSum
#align summable.has_sum_iff_tendsto_nat Summable.hasSum_iff_tendsto_nat

section ContinuousAdd

variable [ContinuousAdd Œ±]

theorem comp_nat_add {f : ‚Ñï ‚Üí Œ±} {k : ‚Ñï} (h : Summable fun n ‚Ü¶ f (n + k)) : Summable f :=
  h.hasSum.sum_range_add.summable

theorem even_add_odd {f : ‚Ñï ‚Üí Œ±} (he : Summable fun k ‚Ü¶ f (2 * k))
    (ho : Summable fun k ‚Ü¶ f (2 * k + 1)) : Summable f :=
  (he.hasSum.even_add_odd ho.hasSum).summable

end ContinuousAdd

end Summable

section tsum

variable [T2Space Œ±]

section Encodable

variable [Encodable Œ≥]

/-- You can compute a sum over an encodable type by summing over the natural numbers and
  taking a supremum. This is useful for outer measures. -/
theorem tsum_iSup_decode‚ÇÇ [CompleteLattice Œ≤] (m : Œ≤ ‚Üí Œ±) (m0 : m ‚ä• = 0) (s : Œ≥ ‚Üí Œ≤) :
    ‚àë' i : ‚Ñï, m (‚®Ü b ‚àà decode‚ÇÇ Œ≥ i, s b) = ‚àë' b : Œ≥, m (s b) := by
  rw [‚Üê tsum_extend_zero (@encode_injective Œ≥ _)]
  refine tsum_congr fun n ‚Ü¶ ?_
  rcases em (n ‚àà Set.range (encode : Œ≥ ‚Üí ‚Ñï)) with ‚ü®a, rfl‚ü© | hn
  ¬∑ simp [encode_injective.extend_apply]
  ¬∑ rw [extend_apply' _ _ _ hn]
    rw [‚Üê decode‚ÇÇ_ne_none_iff, ne_eq, not_not] at hn
    simp [hn, m0]
#align tsum_supr_decode‚ÇÇ tsum_iSup_decode‚ÇÇ

/-- `tsum_iSup_decode‚ÇÇ` specialized to the complete lattice of sets. -/
theorem tsum_iUnion_decode‚ÇÇ (m : Set Œ≤ ‚Üí Œ±) (m0 : m ‚àÖ = 0) (s : Œ≥ ‚Üí Set Œ≤) :
    ‚àë' i, m (‚ãÉ b ‚àà decode‚ÇÇ Œ≥ i, s b) = ‚àë' b, m (s b) :=
  tsum_iSup_decode‚ÇÇ m m0 s
#align tsum_Union_decode‚ÇÇ tsum_iUnion_decode‚ÇÇ

end Encodable

/-! Some properties about measure-like functions. These could also be functions defined on complete
  sublattices of sets, with the property that they are countably sub-additive.
  `R` will probably be instantiated with `(‚â§)` in all applications.
-/
section Countable

variable [Countable Œ≥]

/-- If a function is countably sub-additive then it is sub-additive on countable types -/
theorem rel_iSup_tsum [CompleteLattice Œ≤] (m : Œ≤ ‚Üí Œ±) (m0 : m ‚ä• = 0) (R : Œ± ‚Üí Œ± ‚Üí Prop)
    (m_iSup : ‚àÄ s : ‚Ñï ‚Üí Œ≤, R (m (‚®Ü i, s i)) (‚àë' i, m (s i))) (s : Œ≥ ‚Üí Œ≤) :
    R (m (‚®Ü b : Œ≥, s b)) (‚àë' b : Œ≥, m (s b)) := by
  cases nonempty_encodable Œ≥
  rw [‚Üê iSup_decode‚ÇÇ, ‚Üê tsum_iSup_decode‚ÇÇ _ m0 s]
  exact m_iSup _
#align rel_supr_tsum rel_iSup_tsum

/-- If a function is countably sub-additive then it is sub-additive on finite sets -/
theorem rel_iSup_sum [CompleteLattice Œ≤] (m : Œ≤ ‚Üí Œ±) (m0 : m ‚ä• = 0) (R : Œ± ‚Üí Œ± ‚Üí Prop)
    (m_iSup : ‚àÄ s : ‚Ñï ‚Üí Œ≤, R (m (‚®Ü i, s i)) (‚àë' i, m (s i))) (s : Œ¥ ‚Üí Œ≤) (t : Finset Œ¥) :
    R (m (‚®Ü d ‚àà t, s d)) (‚àë d in t, m (s d)) := by
  rw [iSup_subtype', ‚Üê Finset.tsum_subtype]
  exact rel_iSup_tsum m m0 R m_iSup _
#align rel_supr_sum rel_iSup_sum

/-- If a function is countably sub-additive then it is binary sub-additive -/
theorem rel_sup_add [CompleteLattice Œ≤] (m : Œ≤ ‚Üí Œ±) (m0 : m ‚ä• = 0) (R : Œ± ‚Üí Œ± ‚Üí Prop)
    (m_iSup : ‚àÄ s : ‚Ñï ‚Üí Œ≤, R (m (‚®Ü i, s i)) (‚àë' i, m (s i))) (s‚ÇÅ s‚ÇÇ : Œ≤) :
    R (m (s‚ÇÅ ‚äî s‚ÇÇ)) (m s‚ÇÅ + m s‚ÇÇ) := by
  convert rel_iSup_tsum m m0 R m_iSup fun b ‚Ü¶ cond b s‚ÇÅ s‚ÇÇ
  ¬∑ simp only [iSup_bool_eq, cond]
  ¬∑ rw [tsum_fintype, Fintype.sum_bool, cond, cond]
#align rel_sup_add rel_sup_add

end Countable

section ContinuousAdd

variable [ContinuousAdd Œ±]

theorem sum_add_tsum_nat_add'
    {f : ‚Ñï ‚Üí Œ±} {k : ‚Ñï} (h : Summable (fun n ‚Ü¶ f (n + k))) :
    ((‚àë i in range k, f i) + ‚àë' i, f (i + k)) = ‚àë' i, f i :=
  h.hasSum.sum_range_add.tsum_eq.symm

theorem tsum_eq_zero_add'
    {f : ‚Ñï ‚Üí Œ±} (hf : Summable (fun n ‚Ü¶ f (n + 1))) :
    ‚àë' b, f b = f 0 + ‚àë' b, f (b + 1) := by
  simpa only [sum_range_one] using (sum_add_tsum_nat_add' hf).symm

theorem tsum_even_add_odd {f : ‚Ñï ‚Üí Œ±} (he : Summable fun k ‚Ü¶ f (2 * k))
    (ho : Summable fun k ‚Ü¶ f (2 * k + 1)) :
    ‚àë' k, f (2 * k) + ‚àë' k, f (2 * k + 1) = ‚àë' k, f k :=
  (he.hasSum.even_add_odd ho.hasSum).tsum_eq.symm
#align tsum_even_add_odd tsum_even_add_odd

end ContinuousAdd

end tsum

end Monoid

section TopologicalGroup

variable [AddCommGroup Œ±] [TopologicalSpace Œ±] [TopologicalAddGroup Œ±]

theorem hasSum_nat_add_iff {f : ‚Ñï ‚Üí Œ±} (k : ‚Ñï) {a : Œ±} :
    HasSum (fun n ‚Ü¶ f (n + k)) a ‚Üî HasSum f (a + ‚àë i in range k, f i) := by
  refine' Iff.trans _ (range k).hasSum_compl_iff
  rw [‚Üê (notMemRangeEquiv k).symm.hasSum_iff]
  rfl
#align has_sum_nat_add_iff hasSum_nat_add_iff

theorem summable_nat_add_iff {f : ‚Ñï ‚Üí Œ±} (k : ‚Ñï) : (Summable fun n ‚Ü¶ f (n + k)) ‚Üî Summable f :=
  Iff.symm <|
    (Equiv.addRight (‚àë i in range k, f i)).surjective.summable_iff_of_hasSum_iff
      (hasSum_nat_add_iff k).symm
#align summable_nat_add_iff summable_nat_add_iff

theorem hasSum_nat_add_iff' {f : ‚Ñï ‚Üí Œ±} (k : ‚Ñï) {a : Œ±} :
    HasSum (fun n ‚Ü¶ f (n + k)) (a - ‚àë i in range k, f i) ‚Üî HasSum f a := by
  simp [hasSum_nat_add_iff]
#align has_sum_nat_add_iff' hasSum_nat_add_iff'

theorem sum_add_tsum_nat_add [T2Space Œ±] {f : ‚Ñï ‚Üí Œ±} (k : ‚Ñï) (h : Summable f) :
    ((‚àë i in range k, f i) + ‚àë' i, f (i + k)) = ‚àë' i, f i :=
  sum_add_tsum_nat_add' <| (summable_nat_add_iff k).2 h
#align sum_add_tsum_nat_add sum_add_tsum_nat_add

theorem tsum_eq_zero_add [T2Space Œ±] {f : ‚Ñï ‚Üí Œ±} (hf : Summable f) :
    ‚àë' b, f b = f 0 + ‚àë' b, f (b + 1) :=
  tsum_eq_zero_add' <| (summable_nat_add_iff 1).2 hf
#align tsum_eq_zero_add tsum_eq_zero_add

/-- For `f : ‚Ñï ‚Üí Œ±`, then `‚àë' k, f (k + i)` tends to zero. This does not require a summability
assumption on `f`, as otherwise all sums are zero. -/
theorem tendsto_sum_nat_add [T2Space Œ±] (f : ‚Ñï ‚Üí Œ±) :
    Tendsto (fun i ‚Ü¶ ‚àë' k, f (k + i)) atTop (ùìù 0) := by
  by_cases hf : Summable f
  ¬∑ have h‚ÇÄ : (fun i ‚Ü¶ ‚àë' i, f i - ‚àë j in range i, f j) = fun i ‚Ü¶ ‚àë' k : ‚Ñï, f (k + i) := by
      ext1 i
      rw [sub_eq_iff_eq_add, add_comm, sum_add_tsum_nat_add i hf]
    have h‚ÇÅ : Tendsto (fun _ : ‚Ñï ‚Ü¶ ‚àë' i, f i) atTop (ùìù (‚àë' i, f i)) := tendsto_const_nhds
    simpa only [h‚ÇÄ, sub_self] using Tendsto.sub h‚ÇÅ hf.hasSum.tendsto_sum_nat
  ¬∑ refine tendsto_const_nhds.congr fun n ‚Ü¶ (tsum_eq_zero_of_not_summable ?_).symm
    rwa [summable_nat_add_iff n]
#align tendsto_sum_nat_add tendsto_sum_nat_add

end TopologicalGroup

section UniformGroup

variable [AddCommGroup Œ±] [UniformSpace Œ±] [UniformAddGroup Œ±]

theorem cauchySeq_finset_iff_nat_tsum_vanishing {f : ‚Ñï ‚Üí Œ±} :
    (CauchySeq fun s : Finset ‚Ñï ‚Ü¶ ‚àë n in s, f n) ‚Üî
      ‚àÄ e ‚àà ùìù (0 : Œ±), ‚àÉ N : ‚Ñï, ‚àÄ t ‚äÜ {n | N ‚â§ n}, (‚àë' n : t, f n) ‚àà e := by
  refine cauchySeq_finset_iff_tsum_vanishing.trans ‚ü®fun vanish e he ‚Ü¶ ?_, fun vanish e he ‚Ü¶ ?_‚ü©
  ¬∑ obtain ‚ü®s, hs‚ü© := vanish e he
    refine ‚ü®if h : s.Nonempty then s.max' h + 1 else 0, fun t ht ‚Ü¶ hs _ <| Set.disjoint_left.mpr ?_‚ü©
    split_ifs at ht with h
    ¬∑ exact fun m hmt hms ‚Ü¶ (s.le_max' _ hms).not_lt (Nat.succ_le_iff.mp <| ht hmt)
    ¬∑ exact fun _ _ hs ‚Ü¶ h ‚ü®_, hs‚ü©
  ¬∑ obtain ‚ü®N, hN‚ü© := vanish e he
    exact ‚ü®range N, fun t ht ‚Ü¶ hN _ fun n hnt ‚Ü¶
      le_of_not_lt fun h ‚Ü¶ Set.disjoint_left.mp ht hnt (mem_range.mpr h)‚ü©

variable [CompleteSpace Œ±]

theorem summable_iff_nat_tsum_vanishing {f : ‚Ñï ‚Üí Œ±} : Summable f ‚Üî
    ‚àÄ e ‚àà ùìù (0 : Œ±), ‚àÉ N : ‚Ñï, ‚àÄ t ‚äÜ {n | N ‚â§ n}, (‚àë' n : t, f n) ‚àà e := by
  rw [summable_iff_cauchySeq_finset, cauchySeq_finset_iff_nat_tsum_vanishing]

end UniformGroup

section TopologicalGroup

variable [TopologicalSpace Œ±] [AddCommGroup Œ±] [TopologicalAddGroup Œ±]

theorem Summable.nat_tsum_vanishing {f : ‚Ñï ‚Üí Œ±} (hf : Summable f) ‚¶Ée : Set Œ±‚¶Ñ (he : e ‚àà ùìù 0) :
    ‚àÉ N : ‚Ñï, ‚àÄ t ‚äÜ {n | N ‚â§ n}, (‚àë' n : t, f n) ‚àà e :=
  letI : UniformSpace Œ± := TopologicalAddGroup.toUniformSpace Œ±
  have : UniformAddGroup Œ± := comm_topologicalAddGroup_is_uniform
  cauchySeq_finset_iff_nat_tsum_vanishing.1 hf.hasSum.cauchySeq e he

theorem Summable.tendsto_atTop_zero {f : ‚Ñï ‚Üí Œ±} (hf : Summable f) : Tendsto f atTop (ùìù 0) := by
  rw [‚Üê Nat.cofinite_eq_atTop]
  exact hf.tendsto_cofinite_zero
#align summable.tendsto_at_top_zero Summable.tendsto_atTop_zero

end TopologicalGroup

end Nat

/-!
## Sums over `‚Ñ§`

In this section we prove a variety of lemmas relating sums over `‚Ñï` to sums over `‚Ñ§`.
-/

section Int

section Monoid

variable [AddCommMonoid Œ±] [TopologicalSpace Œ±] {a b : Œ±}

lemma HasSum.natCast_add_negSucc {f : ‚Ñ§ ‚Üí Œ±} (hf : HasSum f a) :
    HasSum (fun n : ‚Ñï ‚Ü¶ f n + f (Int.negSucc n)) a := by
  have : Injective Int.negSucc := @Int.negSucc.inj
  refine hf.hasSum_of_sum_eq fun u ‚Ü¶ ?_
  refine ‚ü®u.preimage _ (Nat.cast_injective.injOn _) ‚à™ u.preimage _ (this.injOn _),
      fun v' hv' ‚Ü¶ ‚ü®v'.image Nat.cast ‚à™ v'.image Int.negSucc, fun x hx ‚Ü¶ ?_, ?_‚ü©‚ü©
  ¬∑ simp only [mem_union, mem_image]
    cases x
    ¬∑ exact Or.inl ‚ü®_, hv' (by simpa using Or.inl hx), rfl‚ü©
    ¬∑ exact Or.inr ‚ü®_, hv' (by simpa using Or.inr hx), rfl‚ü©
  ¬∑ rw [sum_union, sum_image (Nat.cast_injective.injOn _), sum_image (this.injOn _),
      sum_add_distrib]
    simp only [disjoint_iff_ne, mem_image, ne_eq, forall_exists_index, and_imp,
      forall_apply_eq_imp_iff‚ÇÇ, not_false_eq_true, implies_true, forall_const]

lemma Summable.natCast_add_negSucc {f : ‚Ñ§ ‚Üí Œ±} (hf : Summable f) :
    Summable (fun n : ‚Ñï ‚Ü¶ f n + f (Int.negSucc n)) :=
  hf.hasSum.natCast_add_negSucc.summable

lemma tsum_natCast_add_negSucc [T2Space Œ±] {f : ‚Ñ§ ‚Üí Œ±} (hf : Summable f) :
    ‚àë' (n : ‚Ñï), (f n + f (Int.negSucc n)) = ‚àë' (n : ‚Ñ§), f n :=
  hf.hasSum.natCast_add_negSucc.tsum_eq

section ContinuousAdd

variable [ContinuousAdd Œ±]

lemma HasSum.of_natCast_negSucc {f : ‚Ñ§ ‚Üí Œ±}
    (hf‚ÇÅ : HasSum (fun n : ‚Ñï ‚Ü¶ f n) a) (hf‚ÇÇ : HasSum (fun n : ‚Ñï ‚Ü¶ f (Int.negSucc n)) b) :
    HasSum f (a + b) := by
  have hi‚ÇÅ : Injective ((‚Üë) : ‚Ñï ‚Üí ‚Ñ§) := @Int.ofNat.inj
  have hi‚ÇÇ : Injective Int.negSucc := @Int.negSucc.inj
  have : IsCompl (Set.range ((‚Üë) : ‚Ñï ‚Üí ‚Ñ§)) (Set.range Int.negSucc) := by
    constructor
    ¬∑ rw [disjoint_iff_inf_le]
      rintro _ ‚ü®‚ü®i, rfl‚ü©, ‚ü®j, ‚ü®‚ü©‚ü©‚ü©
    ¬∑ rw [codisjoint_iff_le_sup]
      rintro (i | j) <;> simp
  exact (hi‚ÇÅ.hasSum_range_iff.mpr hf‚ÇÅ).add_isCompl this (hi‚ÇÇ.hasSum_range_iff.mpr hf‚ÇÇ)
#align has_sum.nonneg_add_neg HasSum.of_natCast_negSucc

-- deprecated 2024-03-04
@[deprecated] alias HasSum.nonneg_add_neg := HasSum.of_natCast_negSucc

lemma Summable.of_natCast_negSucc {f : ‚Ñ§ ‚Üí Œ±}
    (hf‚ÇÅ : Summable fun n : ‚Ñï ‚Ü¶ f n)  (hf‚ÇÇ : Summable fun n : ‚Ñï ‚Ü¶ f (Int.negSucc n)) :
    Summable f :=
  (hf‚ÇÅ.hasSum.of_natCast_negSucc hf‚ÇÇ.hasSum).summable

lemma tsum_of_natCast_negSucc [T2Space Œ±] {f : ‚Ñ§ ‚Üí Œ±}
    (hf‚ÇÅ : Summable fun n : ‚Ñï ‚Ü¶ f n) (hf‚ÇÇ : Summable fun n : ‚Ñï ‚Ü¶ f (Int.negSucc n)) :
    ‚àë' n : ‚Ñ§, f n = ‚àë' n : ‚Ñï, f n + ‚àë' n : ‚Ñï, f (Int.negSucc n) :=
  (hf‚ÇÅ.hasSum.of_natCast_negSucc hf‚ÇÇ.hasSum).tsum_eq

/-- If `f‚ÇÄ, f‚ÇÅ, f‚ÇÇ, ...` and `g‚ÇÄ, g‚ÇÅ, g‚ÇÇ, ...` have sums `a`, `b` respectively, then the `‚Ñ§`-indexed
sequence: `..., g‚ÇÇ, g‚ÇÅ, g‚ÇÄ, f‚ÇÄ, f‚ÇÅ, f‚ÇÇ, ...` (with `f‚ÇÄ` at the `0`-th position) has sum `a + b`. -/
lemma HasSum.int_rec {f g : ‚Ñï ‚Üí Œ±} (hf : HasSum f a) (hg : HasSum g b) :
    HasSum (Int.rec f g) (a + b) :=
  HasSum.of_natCast_negSucc hf hg
#align has_sum.int_rec HasSum.int_rec

/-- If `f‚ÇÄ, f‚ÇÅ, f‚ÇÇ, ...` and `g‚ÇÄ, g‚ÇÅ, g‚ÇÇ, ...` are both summable then so is the `‚Ñ§`-indexed
sequence: `..., g‚ÇÇ, g‚ÇÅ, g‚ÇÄ, f‚ÇÄ, f‚ÇÅ, f‚ÇÇ, ...` (with `f‚ÇÄ` at the `0`-th position). -/
lemma Summable.int_rec {f g : ‚Ñï ‚Üí Œ±} (hf : Summable f) (hg : Summable g) : Summable (Int.rec f g) :=
  .of_natCast_negSucc hf hg

/-- If `f‚ÇÄ, f‚ÇÅ, f‚ÇÇ, ...` and `g‚ÇÄ, g‚ÇÅ, g‚ÇÇ, ...` are both summable, then the sum of the `‚Ñ§`-indexed
sequence: `..., g‚ÇÇ, g‚ÇÅ, g‚ÇÄ, f‚ÇÄ, f‚ÇÅ, f‚ÇÇ, ...` (with `f‚ÇÄ` at the `0`-th position) is
`‚àë' n, f n + ‚àë' n, g n`. -/
lemma tsum_int_rec [T2Space Œ±] {f g : ‚Ñï ‚Üí Œ±} (hf : Summable f) (hg : Summable g) :
    ‚àë' n : ‚Ñ§, Int.rec f g n = ‚àë' n : ‚Ñï, f n + ‚àë' n : ‚Ñï, g n :=
  (hf.hasSum.int_rec hg.hasSum).tsum_eq

theorem HasSum.natCast_add_neg_natCast {f : ‚Ñ§ ‚Üí Œ±} (hf : HasSum f a) :
    HasSum (fun n : ‚Ñï ‚Ü¶ f n + f (-n)) (a + f 0) := by
  apply (hf.add (hasSum_ite_eq (0 : ‚Ñ§) (f 0))).hasSum_of_sum_eq fun u ‚Ü¶ ?_
  refine' ‚ü®u.image Int.natAbs, fun v' hv' ‚Ü¶ _‚ü©
  let u1 := v'.image fun x : ‚Ñï ‚Ü¶ (x : ‚Ñ§)
  let u2 := v'.image fun x : ‚Ñï ‚Ü¶ -(x : ‚Ñ§)
  have A : u ‚äÜ u1 ‚à™ u2 := by
    intro x hx
    simp only [u1, u2, mem_union, mem_image, exists_prop]
    rcases le_total 0 x with (h'x | h'x)
    ¬∑ left
      refine' ‚ü®Int.natAbs x, hv' _, _‚ü©
      ¬∑ simp only [mem_image, exists_prop]
        exact ‚ü®x, hx, rfl‚ü©
      ¬∑ simp only [h'x, Int.coe_natAbs, abs_eq_self]
    ¬∑ right
      refine' ‚ü®Int.natAbs x, hv' _, _‚ü©
      ¬∑ simp only [mem_image, exists_prop]
        exact ‚ü®x, hx, rfl‚ü©
      ¬∑ simp only [abs_of_nonpos h'x, Int.coe_natAbs, neg_neg]
  refine' ‚ü®u1 ‚à™ u2, A, _‚ü©
  calc
    (‚àë x in u1 ‚à™ u2, (f x + ite (x = 0) (f 0) 0)) =
        (‚àë x in u1 ‚à™ u2, f x) + ‚àë x in u1 ‚à© u2, f x := by
      rw [sum_add_distrib]
      congr 1
      refine' (sum_subset_zero_on_sdiff inter_subset_union _ _).symm
      ¬∑ intro x hx
        suffices x ‚â† 0 by simp only [this, if_false]
        rintro rfl
        simp only [mem_sdiff, mem_union, mem_image, Nat.cast_eq_zero, exists_eq_right, neg_eq_zero,
          or_self, mem_inter, and_self, and_not_self, u1, u2] at hx
      ¬∑ intro x hx
        simp only [u1, u2, mem_inter, mem_image, exists_prop] at hx
        have : x = 0 := by
          apply le_antisymm
          ¬∑ rcases hx.2 with ‚ü®a, _, rfl‚ü©
            simp only [Right.neg_nonpos_iff, Nat.cast_nonneg]
          ¬∑ rcases hx.1 with ‚ü®a, _, rfl‚ü©
            simp only [Nat.cast_nonneg]
        simp only [this, eq_self_iff_true, if_true]
    _ = (‚àë x in u1, f x) + ‚àë x in u2, f x := sum_union_inter
    _ = (‚àë b in v', f b) + ‚àë b in v', f (-b) := by
      simp only [u1, u2, Nat.cast_inj, imp_self, implies_true, forall_const, sum_image, neg_inj]
    _ = ‚àë b in v', (f b + f (-b)) := sum_add_distrib.symm
#align has_sum.sum_nat_of_sum_int HasSum.natCast_add_neg_natCast

-- deprecated 2024-03-04
@[deprecated HasSum.natCast_add_neg_natCast] alias HasSum.sum_nat_of_sum_int :=
  HasSum.natCast_add_neg_natCast

theorem Summable.natCast_add_neg_natCast {f : ‚Ñ§ ‚Üí Œ±} (hf : Summable f) :
    Summable fun n : ‚Ñï ‚Ü¶ f n + f (-n) :=
  hf.hasSum.natCast_add_neg_natCast.summable

lemma tsum_natCast_add_neg_natCast [T2Space Œ±] {f : ‚Ñ§ ‚Üí Œ±} (hf : Summable f) :
    ‚àë' n : ‚Ñï, (f n + f (-n)) = (‚àë' n : ‚Ñ§, f n) + f 0 :=
  hf.hasSum.natCast_add_neg_natCast.tsum_eq

theorem HasSum.of_succ_negSucc {a b : Œ±} {f : ‚Ñ§ ‚Üí Œ±}
    (hf‚ÇÅ : HasSum (fun n : ‚Ñï ‚Ü¶ f (n + 1)) a) (hf‚ÇÇ : HasSum (fun n : ‚Ñï ‚Ü¶ f (Int.negSucc n)) b) :
    HasSum f (a + f 0 + b) :=
  haveI : ‚àÄ g : ‚Ñï ‚Üí Œ±, HasSum (fun k ‚Ü¶ g (k + 1)) a ‚Üí HasSum g (a + g 0) :=
    fun g hg ‚Ü¶ by simpa using (add_comm _ a ‚ñ∏ hg.sum_range_add :)
  (this (fun n ‚Ü¶ f n) hf‚ÇÅ).nonneg_add_neg hf‚ÇÇ
#align has_sum.pos_add_zero_add_neg HasSum.of_succ_negSucc

-- deprecated 2024-03-04
@[deprecated HasSum.of_succ_negSucc] alias HasSum.pos_add_zero_add_neg :=
  HasSum.of_succ_negSucc

lemma Summable.of_succ_negSucc {f : ‚Ñ§ ‚Üí Œ±}
    (hf‚ÇÅ : Summable fun n : ‚Ñï ‚Ü¶ f (n + 1)) (hf‚ÇÇ : Summable fun n : ‚Ñï ‚Ü¶ f (Int.negSucc n)) :
    Summable f :=
  (hf‚ÇÅ.hasSum.of_succ_negSucc hf‚ÇÇ.hasSum).summable

lemma tsum_of_succ_negSucc [T2Space Œ±] {f : ‚Ñ§ ‚Üí Œ±}
    (hf‚ÇÅ : Summable fun n : ‚Ñï ‚Ü¶ f (n + 1)) (hf‚ÇÇ : Summable fun n : ‚Ñï ‚Ü¶ f (Int.negSucc n)) :
    ‚àë' n : ‚Ñ§, f n = ‚àë' n : ‚Ñï, f (n + 1) + f 0 + ‚àë' n : ‚Ñï, f (Int.negSucc n) :=
  (hf‚ÇÅ.hasSum.of_succ_negSucc hf‚ÇÇ.hasSum).tsum_eq

end ContinuousAdd

end Monoid

section TopologicalGroup

variable [AddCommGroup Œ±] [TopologicalSpace Œ±] [TopologicalAddGroup Œ±]

lemma HasSum.of_natCast_neg_natCast {a b : Œ±} {f : ‚Ñ§ ‚Üí Œ±} (hf‚ÇÅ : HasSum (fun n : ‚Ñï ‚Ü¶ f n) a)
    (hf‚ÇÇ : HasSum (fun n : ‚Ñï ‚Ü¶ f (-n)) b) : HasSum f (a + b - f 0) := by
  refine add_sub_assoc' a .. ‚ñ∏ hf‚ÇÅ.nonneg_add_neg (b := b - f 0) ?_
  rwa [‚Üê hasSum_nat_add_iff' 1, sum_range_one, Nat.cast_zero, neg_zero] at hf‚ÇÇ

lemma Summable.of_natCast_neg_natCast {f : ‚Ñ§ ‚Üí Œ±} (hf‚ÇÅ : Summable fun n : ‚Ñï ‚Ü¶ f n)
    (hf‚ÇÇ : Summable fun n : ‚Ñï ‚Ü¶ f (-n)) : Summable f :=
  (hf‚ÇÅ.hasSum.of_natCast_neg_natCast hf‚ÇÇ.hasSum).summable
#align summable_int_of_summable_nat Summable.of_natCast_neg_natCast

-- deprecated 2024-03-04
@[deprecated Summable.of_natCast_neg_natCast] alias summable_int_of_summable_nat :=
  Summable.of_natCast_neg_natCast

lemma tsum_of_natCast_neg_natCast [T2Space Œ±] {f : ‚Ñ§ ‚Üí Œ±}
    (hf‚ÇÅ : Summable fun n : ‚Ñï ‚Ü¶ f n) (hf‚ÇÇ : Summable fun n : ‚Ñï ‚Ü¶ f (-n)) :
    ‚àë' n : ‚Ñ§, f n = ‚àë' n : ‚Ñï, f n + ‚àë' n : ‚Ñï, f (-n) - f 0 :=
  (hf‚ÇÅ.hasSum.of_natCast_neg_natCast hf‚ÇÇ.hasSum).tsum_eq

end TopologicalGroup

section UniformGroup -- results which depend on completeness

variable [AddCommGroup Œ±] [UniformSpace Œ±] [UniformAddGroup Œ±] [CompleteSpace Œ±]

/-- "iff" version of `Summable.of_natCast_negSucc`. -/
lemma summable_int_iff_summable_natCast_negSucc {f : ‚Ñ§ ‚Üí Œ±} :
    Summable f ‚Üî (Summable fun n : ‚Ñï ‚Ü¶ f n) ‚àß (Summable fun n : ‚Ñï ‚Ü¶ f (Int.negSucc n)) := by
  refine ‚ü®fun p ‚Ü¶ ‚ü®?_, ?_‚ü©, fun ‚ü®hf‚ÇÅ, hf‚ÇÇ‚ü© ‚Ü¶ Summable.of_natCast_negSucc hf‚ÇÅ hf‚ÇÇ‚ü© <;>
  apply p.comp_injective
  exacts [Nat.cast_injective, @Int.negSucc.inj]

/-- "iff" version of `Summable.of_natCast_neg_natCast`. -/
lemma summable_int_iff_summable_natCast_neg_natCast {f : ‚Ñ§ ‚Üí Œ±} :
    Summable f ‚Üî (Summable fun n : ‚Ñï ‚Ü¶ f n) ‚àß (Summable fun n : ‚Ñï ‚Ü¶ f (-n)) := by
  refine ‚ü®fun p ‚Ü¶ ‚ü®?_, ?_‚ü©, fun ‚ü®hf‚ÇÅ, hf‚ÇÇ‚ü© ‚Ü¶ Summable.of_natCast_neg_natCast hf‚ÇÅ hf‚ÇÇ‚ü© <;>
  apply p.comp_injective
  exacts [Nat.cast_injective, neg_injective.comp Nat.cast_injective]

end UniformGroup

end Int
