/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl
-/

import Mathlib.Logic.Encodable.Lattice
import Mathlib.Order.Filter.AtTopBot.Finset
import Mathlib.Topology.Algebra.InfiniteSum.Group

/-!
# Infinite sums and products over `‚Ñï` and `‚Ñ§`

This file contains lemmas about `HasSum`, `Summable`, `tsum`, `HasProd`, `Multipliable`, and `tprod`
applied to the important special cases where the domain is `‚Ñï` or `‚Ñ§`. For instance, we prove the
formula `‚àë i ‚àà range k, f i + ‚àë' i, f (i + k) = ‚àë' i, f i`, ‚àà `sum_add_tsum_nat_add`, as well as
several results relating sums and products on `‚Ñï` to sums and products on `‚Ñ§`.
-/

noncomputable section

open Filter Finset Function Encodable

open scoped Topology

variable {M : Type*} [CommMonoid M] [TopologicalSpace M] {m m' : M}

variable {G : Type*} [CommGroup G] {g g' : G}
-- don't declare `[IsTopologicalAddGroup G]`, here as some results require
-- `[IsUniformAddGroup G]` instead

/-!
## Sums over `‚Ñï`
-/

section Nat

section Monoid

/-- If `f : ‚Ñï ‚Üí M` has product `m`, then the partial products `‚àè i ‚àà range n, f i` converge
to `m`. -/
@[to_additive /-- If `f : ‚Ñï ‚Üí M` has sum `m`, then the partial sums `‚àë i ‚àà range n, f i` converge
to `m`. -/]
theorem HasProd.tendsto_prod_nat {f : ‚Ñï ‚Üí M} (h : HasProd f m) :
    Tendsto (fun n ‚Ü¶ ‚àè i ‚àà range n, f i) atTop (ùìù m) :=
  h.comp tendsto_finset_range

/-- If `f : ‚Ñï ‚Üí M` is multipliable, then the partial products `‚àè i ‚àà range n, f i` converge
to `‚àè' i, f i`. -/
@[to_additive /-- If `f : ‚Ñï ‚Üí M` is summable, then the partial sums `‚àë i ‚àà range n, f i` converge
to `‚àë' i, f i`. -/]
theorem Multipliable.tendsto_prod_tprod_nat {f : ‚Ñï ‚Üí M} (h : Multipliable f) :
    Tendsto (fun n ‚Ü¶ ‚àè i ‚àà range n, f i) atTop (ùìù (‚àè' i, f i)) :=
  h.hasProd.tendsto_prod_nat

namespace HasProd

section ContinuousMul

variable [ContinuousMul M]

@[to_additive]
theorem prod_range_mul {f : ‚Ñï ‚Üí M} {k : ‚Ñï} (h : HasProd (fun n ‚Ü¶ f (n + k)) m) :
    HasProd f ((‚àè i ‚àà range k, f i) * m) := by
  refine ((range k).hasProd f).mul_compl ?_
  rwa [‚Üê (notMemRangeEquiv k).symm.hasProd_iff]

@[to_additive]
theorem zero_mul {f : ‚Ñï ‚Üí M} (h : HasProd (fun n ‚Ü¶ f (n + 1)) m) :
    HasProd f (f 0 * m) := by
  simpa only [prod_range_one] using h.prod_range_mul

@[to_additive]
theorem even_mul_odd {f : ‚Ñï ‚Üí M} (he : HasProd (fun k ‚Ü¶ f (2 * k)) m)
    (ho : HasProd (fun k ‚Ü¶ f (2 * k + 1)) m') : HasProd f (m * m') := by
  have := mul_right_injective‚ÇÄ (two_ne_zero' ‚Ñï)
  replace ho := ((add_left_injective 1).comp this).hasProd_range_iff.2 ho
  refine (this.hasProd_range_iff.2 he).mul_isCompl ?_ ho
  simpa [Function.comp_def] using Nat.isCompl_even_odd

end ContinuousMul

end HasProd

namespace Multipliable

@[to_additive]
theorem hasProd_iff_tendsto_nat [T2Space M] {f : ‚Ñï ‚Üí M} (hf : Multipliable f) :
    HasProd f m ‚Üî Tendsto (fun n : ‚Ñï ‚Ü¶ ‚àè i ‚àà range n, f i) atTop (ùìù m) := by
  refine ‚ü®fun h ‚Ü¶ h.tendsto_prod_nat, fun h ‚Ü¶ ?_‚ü©
  rw [tendsto_nhds_unique h hf.hasProd.tendsto_prod_nat]
  exact hf.hasProd

section ContinuousMul

variable [ContinuousMul M]

@[to_additive]
theorem comp_nat_add {f : ‚Ñï ‚Üí M} {k : ‚Ñï} (h : Multipliable fun n ‚Ü¶ f (n + k)) : Multipliable f :=
  h.hasProd.prod_range_mul.multipliable

@[to_additive]
theorem even_mul_odd {f : ‚Ñï ‚Üí M} (he : Multipliable fun k ‚Ü¶ f (2 * k))
    (ho : Multipliable fun k ‚Ü¶ f (2 * k + 1)) : Multipliable f :=
  (he.hasProd.even_mul_odd ho.hasProd).multipliable

end ContinuousMul

end Multipliable

section tprod

variable {Œ± Œ≤ Œ≥ : Type*}

section Encodable

variable [Encodable Œ≤]

/-- You can compute a product over an encodable type by multiplying over the natural numbers and
taking a supremum. -/
@[to_additive /-- You can compute a sum over an encodable type by summing over the natural numbers
and taking a supremum. This is useful for outer measures. -/]
theorem tprod_iSup_decode‚ÇÇ [CompleteLattice Œ±] (m : Œ± ‚Üí M) (m0 : m ‚ä• = 1) (s : Œ≤ ‚Üí Œ±) :
    ‚àè' i : ‚Ñï, m (‚®Ü b ‚àà decode‚ÇÇ Œ≤ i, s b) = ‚àè' b : Œ≤, m (s b) := by
  rw [‚Üê tprod_extend_one (@encode_injective Œ≤ _)]
  refine tprod_congr fun n ‚Ü¶ ?_
  rcases em (n ‚àà Set.range (encode : Œ≤ ‚Üí ‚Ñï)) with ‚ü®a, rfl‚ü© | hn
  ¬∑ simp [encode_injective.extend_apply]
  ¬∑ rw [extend_apply' _ _ _ hn]
    rw [‚Üê decode‚ÇÇ_ne_none_iff, ne_eq, not_not] at hn
    simp [hn, m0]

/-- `tprod_iSup_decode‚ÇÇ` specialized to the complete lattice of sets. -/
@[to_additive /-- `tsum_iSup_decode‚ÇÇ` specialized to the complete lattice of sets. -/]
theorem tprod_iUnion_decode‚ÇÇ (m : Set Œ± ‚Üí M) (m0 : m ‚àÖ = 1) (s : Œ≤ ‚Üí Set Œ±) :
    ‚àè' i, m (‚ãÉ b ‚àà decode‚ÇÇ Œ≤ i, s b) = ‚àè' b, m (s b) :=
  tprod_iSup_decode‚ÇÇ m m0 s

end Encodable

/-! Some properties about measure-like functions. These could also be functions defined on complete
  sublattices of sets, with the property that they are countably sub-additive.
  `R` will probably be instantiated with `(‚â§)` in all applications.
-/
section Countable

variable [Countable Œ≤]

/-- If a function is countably sub-multiplicative then it is sub-multiplicative on countable
types -/
@[to_additive
/-- If a function is countably sub-additive then it is sub-additive on countable types -/]
theorem rel_iSup_tprod [CompleteLattice Œ±] (m : Œ± ‚Üí M) (m0 : m ‚ä• = 1) (R : M ‚Üí M ‚Üí Prop)
    (m_iSup : ‚àÄ s : ‚Ñï ‚Üí Œ±, R (m (‚®Ü i, s i)) (‚àè' i, m (s i))) (s : Œ≤ ‚Üí Œ±) :
    R (m (‚®Ü b : Œ≤, s b)) (‚àè' b : Œ≤, m (s b)) := by
  cases nonempty_encodable Œ≤
  rw [‚Üê iSup_decode‚ÇÇ, ‚Üê tprod_iSup_decode‚ÇÇ _ m0 s]
  exact m_iSup _

/-- If a function is countably sub-multiplicative then it is sub-multiplicative on finite sets -/
@[to_additive /-- If a function is countably sub-additive then it is sub-additive on finite sets -/]
theorem rel_iSup_prod [CompleteLattice Œ±] (m : Œ± ‚Üí M) (m0 : m ‚ä• = 1) (R : M ‚Üí M ‚Üí Prop)
    (m_iSup : ‚àÄ s : ‚Ñï ‚Üí Œ±, R (m (‚®Ü i, s i)) (‚àè' i, m (s i))) (s : Œ≥ ‚Üí Œ±) (t : Finset Œ≥) :
    R (m (‚®Ü d ‚àà t, s d)) (‚àè d ‚àà t, m (s d)) := by
  rw [iSup_subtype', ‚Üê Finset.tprod_subtype]
  exact rel_iSup_tprod m m0 R m_iSup _

/-- If a function is countably sub-multiplicative then it is binary sub-multiplicative -/
@[to_additive /-- If a function is countably sub-additive then it is binary sub-additive -/]
theorem rel_sup_mul [CompleteLattice Œ±] (m : Œ± ‚Üí M) (m0 : m ‚ä• = 1) (R : M ‚Üí M ‚Üí Prop)
    (m_iSup : ‚àÄ s : ‚Ñï ‚Üí Œ±, R (m (‚®Ü i, s i)) (‚àè' i, m (s i))) (s‚ÇÅ s‚ÇÇ : Œ±) :
    R (m (s‚ÇÅ ‚äî s‚ÇÇ)) (m s‚ÇÅ * m s‚ÇÇ) := by
  convert rel_iSup_tprod m m0 R m_iSup fun b ‚Ü¶ cond b s‚ÇÅ s‚ÇÇ
  ¬∑ simp only [iSup_bool_eq, cond]
  ¬∑ rw [tprod_fintype, Fintype.prod_bool, cond, cond]

end Countable

section ContinuousMul

variable [T2Space M] [ContinuousMul M]

@[to_additive]
protected theorem Multipliable.prod_mul_tprod_nat_mul'
    {f : ‚Ñï ‚Üí M} {k : ‚Ñï} (h : Multipliable (fun n ‚Ü¶ f (n + k))) :
    ((‚àè i ‚àà range k, f i) * ‚àè' i, f (i + k)) = ‚àè' i, f i :=
  h.hasProd.prod_range_mul.tprod_eq.symm

@[deprecated (since := "2025-04-12")] alias sum_add_tsum_nat_add' := Summable.sum_add_tsum_nat_add'
@[to_additive existing, deprecated (since := "2025-04-12")] alias prod_mul_tprod_nat_mul' :=
  Multipliable.prod_mul_tprod_nat_mul'

@[to_additive]
theorem tprod_eq_zero_mul'
    {f : ‚Ñï ‚Üí M} (hf : Multipliable (fun n ‚Ü¶ f (n + 1))) :
    ‚àè' b, f b = f 0 * ‚àè' b, f (b + 1) := by
  simpa only [prod_range_one] using hf.prod_mul_tprod_nat_mul'.symm

@[to_additive]
theorem tprod_even_mul_odd {f : ‚Ñï ‚Üí M} (he : Multipliable fun k ‚Ü¶ f (2 * k))
    (ho : Multipliable fun k ‚Ü¶ f (2 * k + 1)) :
    (‚àè' k, f (2 * k)) * ‚àè' k, f (2 * k + 1) = ‚àè' k, f k :=
  (he.hasProd.even_mul_odd ho.hasProd).tprod_eq.symm

end ContinuousMul

end tprod

end Monoid

section IsTopologicalGroup

variable [TopologicalSpace G] [IsTopologicalGroup G]

@[to_additive]
theorem hasProd_nat_add_iff {f : ‚Ñï ‚Üí G} (k : ‚Ñï) :
    HasProd (fun n ‚Ü¶ f (n + k)) g ‚Üî HasProd f (g * ‚àè i ‚àà range k, f i) := by
  refine Iff.trans ?_ (range k).hasProd_compl_iff
  rw [‚Üê (notMemRangeEquiv k).symm.hasProd_iff, Function.comp_def, coe_notMemRangeEquiv_symm]

@[to_additive]
theorem multipliable_nat_add_iff {f : ‚Ñï ‚Üí G} (k : ‚Ñï) :
    (Multipliable fun n ‚Ü¶ f (n + k)) ‚Üî Multipliable f :=
  Iff.symm <|
    (Equiv.mulRight (‚àè i ‚àà range k, f i)).surjective.multipliable_iff_of_hasProd_iff
      (hasProd_nat_add_iff k).symm

@[to_additive]
theorem hasProd_nat_add_iff' {f : ‚Ñï ‚Üí G} (k : ‚Ñï) :
    HasProd (fun n ‚Ü¶ f (n + k)) (g / ‚àè i ‚àà range k, f i) ‚Üî HasProd f g := by
  simp [hasProd_nat_add_iff]

@[to_additive]
protected theorem Multipliable.prod_mul_tprod_nat_add [T2Space G] {f : ‚Ñï ‚Üí G} (k : ‚Ñï)
    (h : Multipliable f) : ((‚àè i ‚àà range k, f i) * ‚àè' i, f (i + k)) = ‚àè' i, f i :=
  Multipliable.prod_mul_tprod_nat_mul' <| (multipliable_nat_add_iff k).2 h

@[deprecated (since := "2025-04-12")] alias sum_add_tsum_nat_add :=
  Summable.sum_add_tsum_nat_add
@[to_additive existing, deprecated (since := "2025-04-12")] alias prod_mul_tprod_nat_add :=
  Multipliable.prod_mul_tprod_nat_add

@[to_additive]
protected theorem Multipliable.tprod_eq_zero_mul [T2Space G] {f : ‚Ñï ‚Üí G} (hf : Multipliable f) :
    ‚àè' b, f b = f 0 * ‚àè' b, f (b + 1) :=
  tprod_eq_zero_mul' <| (multipliable_nat_add_iff 1).2 hf

@[deprecated (since := "2025-04-12")] alias tsum_eq_zero_add := Summable.tsum_eq_zero_add
@[to_additive existing, deprecated (since := "2025-04-12")] alias tprod_eq_zero_mul :=
  Multipliable.tprod_eq_zero_mul

/-- For `f : ‚Ñï ‚Üí G`, the product `‚àè' k, f (k + i)` tends to one. This does not require a
multipliability assumption on `f`, as otherwise all such products are one. -/
@[to_additive /-- For `f : ‚Ñï ‚Üí G`, the sum `‚àë' k, f (k + i)` tends to zero. This does not require a
summability assumption on `f`, as otherwise all such sums are zero. -/]
theorem tendsto_prod_nat_add [T2Space G] (f : ‚Ñï ‚Üí G) :
    Tendsto (fun i ‚Ü¶ ‚àè' k, f (k + i)) atTop (ùìù 1) := by
  by_cases hf : Multipliable f
  ¬∑ have h‚ÇÄ : (fun i ‚Ü¶ (‚àè' i, f i) / ‚àè j ‚àà range i, f j) = fun i ‚Ü¶ ‚àè' k : ‚Ñï, f (k + i) := by
      ext1 i
      rw [div_eq_iff_eq_mul, mul_comm, hf.prod_mul_tprod_nat_add i]
    have h‚ÇÅ : Tendsto (fun _ : ‚Ñï ‚Ü¶ ‚àè' i, f i) atTop (ùìù (‚àè' i, f i)) := tendsto_const_nhds
    simpa only [h‚ÇÄ, div_self'] using Tendsto.div' h‚ÇÅ hf.hasProd.tendsto_prod_nat
  ¬∑ refine tendsto_const_nhds.congr fun n ‚Ü¶ (tprod_eq_one_of_not_multipliable ?_).symm
    rwa [multipliable_nat_add_iff n]

end IsTopologicalGroup

section IsUniformGroup

variable [UniformSpace G] [IsUniformGroup G]

@[to_additive]
theorem cauchySeq_finset_iff_nat_tprod_vanishing {f : ‚Ñï ‚Üí G} :
    (CauchySeq fun s : Finset ‚Ñï ‚Ü¶ ‚àè n ‚àà s, f n) ‚Üî
      ‚àÄ e ‚àà ùìù (1 : G), ‚àÉ N : ‚Ñï, ‚àÄ t ‚äÜ {n | N ‚â§ n}, (‚àè' n : t, f n) ‚àà e := by
  refine cauchySeq_finset_iff_tprod_vanishing.trans ‚ü®fun vanish e he ‚Ü¶ ?_, fun vanish e he ‚Ü¶ ?_‚ü©
  ¬∑ obtain ‚ü®s, hs‚ü© := vanish e he
    refine ‚ü®if h : s.Nonempty then s.max' h + 1 else 0,
      fun t ht ‚Ü¶ hs _ <| Set.disjoint_left.mpr ?_‚ü©
    split_ifs at ht with h
    ¬∑ exact fun m hmt hms ‚Ü¶ (s.le_max' _ hms).not_gt (Nat.succ_le_iff.mp <| ht hmt)
    ¬∑ exact fun _ _ hs ‚Ü¶ h ‚ü®_, hs‚ü©
  ¬∑ obtain ‚ü®N, hN‚ü© := vanish e he
    exact ‚ü®range N, fun t ht ‚Ü¶ hN _ fun n hnt ‚Ü¶
      le_of_not_gt fun h ‚Ü¶ Set.disjoint_left.mp ht hnt (mem_range.mpr h)‚ü©

variable [CompleteSpace G]

@[to_additive]
theorem multipliable_iff_nat_tprod_vanishing {f : ‚Ñï ‚Üí G} : Multipliable f ‚Üî
    ‚àÄ e ‚àà ùìù 1, ‚àÉ N : ‚Ñï, ‚àÄ t ‚äÜ {n | N ‚â§ n}, (‚àè' n : t, f n) ‚àà e := by
  rw [multipliable_iff_cauchySeq_finset, cauchySeq_finset_iff_nat_tprod_vanishing]

end IsUniformGroup

section IsTopologicalGroup

variable [TopologicalSpace G] [IsTopologicalGroup G]

@[to_additive]
theorem Multipliable.nat_tprod_vanishing {f : ‚Ñï ‚Üí G} (hf : Multipliable f) ‚¶Ée : Set G‚¶Ñ
    (he : e ‚àà ùìù 1) : ‚àÉ N : ‚Ñï, ‚àÄ t ‚äÜ {n | N ‚â§ n}, (‚àè' n : t, f n) ‚àà e :=
  letI : UniformSpace G := IsTopologicalGroup.toUniformSpace G
  have : IsUniformGroup G := isUniformGroup_of_commGroup
  cauchySeq_finset_iff_nat_tprod_vanishing.1 hf.hasProd.cauchySeq e he

@[to_additive]
theorem Multipliable.tendsto_atTop_one {f : ‚Ñï ‚Üí G} (hf : Multipliable f) :
    Tendsto f atTop (ùìù 1) := by
  rw [‚Üê Nat.cofinite_eq_atTop]
  exact hf.tendsto_cofinite_one

end IsTopologicalGroup

end Nat

/-!
## Sums over `‚Ñ§`

In this section we prove a variety of lemmas relating sums over `‚Ñï` to sums over `‚Ñ§`.
-/

section Int

section Monoid

@[to_additive HasSum.nat_add_neg_add_one]
lemma HasProd.nat_mul_neg_add_one {f : ‚Ñ§ ‚Üí M} (hf : HasProd f m) :
    HasProd (fun n : ‚Ñï ‚Ü¶ f n * f (-(n + 1))) m := by
  change HasProd (fun n : ‚Ñï ‚Ü¶ f n * f (Int.negSucc n)) m
  have : Injective Int.negSucc := @Int.negSucc.inj
  refine hf.hasProd_of_prod_eq fun u ‚Ü¶ ?_
  refine ‚ü®u.preimage _ Nat.cast_injective.injOn ‚à™ u.preimage _ this.injOn,
      fun v' hv' ‚Ü¶ ‚ü®v'.image Nat.cast ‚à™ v'.image Int.negSucc, fun x hx ‚Ü¶ ?_, ?_‚ü©‚ü©
  ¬∑ simp only [mem_union, mem_image]
    cases x
    ¬∑ exact Or.inl ‚ü®_, hv' (by simpa using Or.inl hx), rfl‚ü©
    ¬∑ exact Or.inr ‚ü®_, hv' (by simpa using Or.inr hx), rfl‚ü©
  ¬∑ rw [prod_union, prod_image Nat.cast_injective.injOn, prod_image this.injOn,
      prod_mul_distrib]
    simp only [disjoint_iff_ne, mem_image, ne_eq, forall_exists_index, and_imp,
      forall_apply_eq_imp_iff‚ÇÇ, not_false_eq_true, implies_true, reduceCtorEq]

@[to_additive Summable.nat_add_neg_add_one]
lemma Multipliable.nat_mul_neg_add_one {f : ‚Ñ§ ‚Üí M} (hf : Multipliable f) :
    Multipliable (fun n : ‚Ñï ‚Ü¶ f n * f (-(n + 1))) :=
  hf.hasProd.nat_mul_neg_add_one.multipliable

@[to_additive tsum_nat_add_neg_add_one]
lemma tprod_nat_mul_neg_add_one [T2Space M] {f : ‚Ñ§ ‚Üí M} (hf : Multipliable f) :
    ‚àè' (n : ‚Ñï), (f n * f (-(n + 1))) = ‚àè' (n : ‚Ñ§), f n :=
  hf.hasProd.nat_mul_neg_add_one.tprod_eq

section ContinuousMul

variable [ContinuousMul M]

@[to_additive HasSum.of_nat_of_neg_add_one]
lemma HasProd.of_nat_of_neg_add_one {f : ‚Ñ§ ‚Üí M}
    (hf‚ÇÅ : HasProd (fun n : ‚Ñï ‚Ü¶ f n) m) (hf‚ÇÇ : HasProd (fun n : ‚Ñï ‚Ü¶ f (-(n + 1))) m') :
    HasProd f (m * m') := by
  have hi‚ÇÇ : Injective Int.negSucc := @Int.negSucc.inj
  have : IsCompl (Set.range ((‚Üë) : ‚Ñï ‚Üí ‚Ñ§)) (Set.range Int.negSucc) := by
    constructor
    ¬∑ rw [disjoint_iff_inf_le]
      rintro _ ‚ü®‚ü®i, rfl‚ü©, ‚ü®j, ‚ü®‚ü©‚ü©‚ü©
    ¬∑ rw [codisjoint_iff_le_sup]
      rintro (i | j) <;> simp
  exact (Nat.cast_injective.hasProd_range_iff.mpr hf‚ÇÅ).mul_isCompl
    this (hi‚ÇÇ.hasProd_range_iff.mpr hf‚ÇÇ)


@[to_additive Summable.of_nat_of_neg_add_one]
lemma Multipliable.of_nat_of_neg_add_one {f : ‚Ñ§ ‚Üí M}
    (hf‚ÇÅ : Multipliable fun n : ‚Ñï ‚Ü¶ f n) (hf‚ÇÇ : Multipliable fun n : ‚Ñï ‚Ü¶ f (-(n + 1))) :
    Multipliable f :=
  (hf‚ÇÅ.hasProd.of_nat_of_neg_add_one hf‚ÇÇ.hasProd).multipliable

@[to_additive tsum_of_nat_of_neg_add_one]
lemma tprod_of_nat_of_neg_add_one [T2Space M] {f : ‚Ñ§ ‚Üí M}
    (hf‚ÇÅ : Multipliable fun n : ‚Ñï ‚Ü¶ f n) (hf‚ÇÇ : Multipliable fun n : ‚Ñï ‚Ü¶ f (-(n + 1))) :
    ‚àè' n : ‚Ñ§, f n = (‚àè' n : ‚Ñï, f n) * ‚àè' n : ‚Ñï, f (-(n + 1)) :=
  (hf‚ÇÅ.hasProd.of_nat_of_neg_add_one hf‚ÇÇ.hasProd).tprod_eq

/-- If `f‚ÇÄ, f‚ÇÅ, f‚ÇÇ, ...` and `g‚ÇÄ, g‚ÇÅ, g‚ÇÇ, ...` have products `a`, `b` respectively, then
the `‚Ñ§`-indexed sequence: `..., g‚ÇÇ, g‚ÇÅ, g‚ÇÄ, f‚ÇÄ, f‚ÇÅ, f‚ÇÇ, ...` (with `f‚ÇÄ` at the `0`-th position) has
product `a + b`. -/
@[to_additive /-- If `f‚ÇÄ, f‚ÇÅ, f‚ÇÇ, ...` and `g‚ÇÄ, g‚ÇÅ, g‚ÇÇ, ...` have sums `a`, `b` respectively, then
the `‚Ñ§`-indexed sequence: `..., g‚ÇÇ, g‚ÇÅ, g‚ÇÄ, f‚ÇÄ, f‚ÇÅ, f‚ÇÇ, ...` (with `f‚ÇÄ` at the `0`-th position) has
sum `a + b`. -/]
lemma HasProd.int_rec {f g : ‚Ñï ‚Üí M} (hf : HasProd f m) (hg : HasProd g m') :
    HasProd (Int.rec f g) (m * m') :=
  HasProd.of_nat_of_neg_add_one hf hg

/-- If `f‚ÇÄ, f‚ÇÅ, f‚ÇÇ, ...` and `g‚ÇÄ, g‚ÇÅ, g‚ÇÇ, ...` are both multipliable then so is the
`‚Ñ§`-indexed sequence: `..., g‚ÇÇ, g‚ÇÅ, g‚ÇÄ, f‚ÇÄ, f‚ÇÅ, f‚ÇÇ, ...` (with `f‚ÇÄ` at the `0`-th position). -/
@[to_additive /-- If `f‚ÇÄ, f‚ÇÅ, f‚ÇÇ, ...` and `g‚ÇÄ, g‚ÇÅ, g‚ÇÇ, ...` are both summable then so is the
`‚Ñ§`-indexed sequence: `..., g‚ÇÇ, g‚ÇÅ, g‚ÇÄ, f‚ÇÄ, f‚ÇÅ, f‚ÇÇ, ...` (with `f‚ÇÄ` at the `0`-th position). -/]
lemma Multipliable.int_rec {f g : ‚Ñï ‚Üí M} (hf : Multipliable f) (hg : Multipliable g) :
    Multipliable (Int.rec f g) :=
  .of_nat_of_neg_add_one hf hg

/-- If `f‚ÇÄ, f‚ÇÅ, f‚ÇÇ, ...` and `g‚ÇÄ, g‚ÇÅ, g‚ÇÇ, ...` are both multipliable, then the product of the
`‚Ñ§`-indexed sequence: `..., g‚ÇÇ, g‚ÇÅ, g‚ÇÄ, f‚ÇÄ, f‚ÇÅ, f‚ÇÇ, ...` (with `f‚ÇÄ` at the `0`-th position) is
`(‚àè' n, f n) * ‚àè' n, g n`. -/
@[to_additive /-- If `f‚ÇÄ, f‚ÇÅ, f‚ÇÇ, ...` and `g‚ÇÄ, g‚ÇÅ, g‚ÇÇ, ...` are both summable, then the sum of the
`‚Ñ§`-indexed sequence: `..., g‚ÇÇ, g‚ÇÅ, g‚ÇÄ, f‚ÇÄ, f‚ÇÅ, f‚ÇÇ, ...` (with `f‚ÇÄ` at the `0`-th position) is
`‚àë' n, f n + ‚àë' n, g n`. -/]
lemma tprod_int_rec [T2Space M] {f g : ‚Ñï ‚Üí M} (hf : Multipliable f) (hg : Multipliable g) :
    ‚àè' n : ‚Ñ§, Int.rec f g n = (‚àè' n : ‚Ñï, f n) * ‚àè' n : ‚Ñï, g n :=
  (hf.hasProd.int_rec hg.hasProd).tprod_eq

@[to_additive]
theorem HasProd.nat_mul_neg {f : ‚Ñ§ ‚Üí M} (hf : HasProd f m) :
    HasProd (fun n : ‚Ñï ‚Ü¶ f n * f (-n)) (m * f 0) := by
  -- Note this is much easier to prove if you assume more about the target space, but we have to
  -- work hard to prove it under the very minimal assumptions here.
  apply (hf.mul (hasProd_ite_eq (0 : ‚Ñ§) (f 0))).hasProd_of_prod_eq fun u ‚Ü¶ ?_
  refine ‚ü®u.image Int.natAbs, fun v' hv' ‚Ü¶ ?_‚ü©
  let u1 := v'.image fun x : ‚Ñï ‚Ü¶ (x : ‚Ñ§)
  let u2 := v'.image fun x : ‚Ñï ‚Ü¶ -(x : ‚Ñ§)
  have A : u ‚äÜ u1 ‚à™ u2 := by
    intro x hx
    simp only [u1, u2, mem_union, mem_image]
    rcases le_total 0 x with (h'x | h'x)
    ¬∑ refine Or.inl ‚ü®_, hv' <| mem_image.mpr ‚ü®x, hx, rfl‚ü©, ?_‚ü©
      simp only [Int.natCast_natAbs, abs_eq_self, h'x]
    ¬∑ refine Or.inr ‚ü®_, hv' <| mem_image.mpr ‚ü®x, hx, rfl‚ü©, ?_‚ü©
      simp only [abs_of_nonpos h'x, Int.natCast_natAbs, neg_neg]
  exact ‚ü®_, A, calc
    (‚àè x ‚àà u1 ‚à™ u2, (f x * if x = 0 then f 0 else 1)) =
        (‚àè x ‚àà u1 ‚à™ u2, f x) * ‚àè x ‚àà u1 ‚à© u2, f x := by
      rw [prod_mul_distrib]
      congr 1
      refine (prod_subset_one_on_sdiff inter_subset_union ?_ ?_).symm
      ¬∑ intro x hx
        suffices x ‚â† 0 by simp only [this, if_false]
        rintro rfl
        simp only [mem_sdiff, mem_union, mem_image, Nat.cast_eq_zero, exists_eq_right, neg_eq_zero,
          or_self, mem_inter, and_self, and_not_self, u1, u2] at hx
      ¬∑ intro x hx
        simp only [u1, u2, mem_inter, mem_image] at hx
        suffices x = 0 by simp only [this, if_true]
        cutsat
    _ = (‚àè x ‚àà u1, f x) * ‚àè x ‚àà u2, f x := prod_union_inter
    _ = (‚àè b ‚àà v', f b) * ‚àè b ‚àà v', f (-b) := by simp [u1, u2]
    _ = ‚àè b ‚àà v', (f b * f (-b)) := prod_mul_distrib.symm‚ü©

@[to_additive]
theorem Multipliable.nat_mul_neg {f : ‚Ñ§ ‚Üí M} (hf : Multipliable f) :
    Multipliable fun n : ‚Ñï ‚Ü¶ f n * f (-n) :=
  hf.hasProd.nat_mul_neg.multipliable

@[to_additive]
lemma tprod_nat_mul_neg [T2Space M] {f : ‚Ñ§ ‚Üí M} (hf : Multipliable f) :
    ‚àè' n : ‚Ñï, (f n * f (-n)) = (‚àè' n : ‚Ñ§, f n) * f 0 :=
  hf.hasProd.nat_mul_neg.tprod_eq

@[to_additive HasSum.of_add_one_of_neg_add_one]
theorem HasProd.of_add_one_of_neg_add_one {f : ‚Ñ§ ‚Üí M}
    (hf‚ÇÅ : HasProd (fun n : ‚Ñï ‚Ü¶ f (n + 1)) m) (hf‚ÇÇ : HasProd (fun n : ‚Ñï ‚Ü¶ f (-(n + 1))) m') :
    HasProd f (m * f 0 * m') :=
  HasProd.of_nat_of_neg_add_one (mul_comm _ m ‚ñ∏ HasProd.zero_mul hf‚ÇÅ) hf‚ÇÇ

@[to_additive Summable.of_add_one_of_neg_add_one]
lemma Multipliable.of_add_one_of_neg_add_one {f : ‚Ñ§ ‚Üí M}
    (hf‚ÇÅ : Multipliable fun n : ‚Ñï ‚Ü¶ f (n + 1)) (hf‚ÇÇ : Multipliable fun n : ‚Ñï ‚Ü¶ f (-(n + 1))) :
    Multipliable f :=
  (hf‚ÇÅ.hasProd.of_add_one_of_neg_add_one hf‚ÇÇ.hasProd).multipliable

@[to_additive tsum_of_add_one_of_neg_add_one]
lemma tprod_of_add_one_of_neg_add_one [T2Space M] {f : ‚Ñ§ ‚Üí M}
    (hf‚ÇÅ : Multipliable fun n : ‚Ñï ‚Ü¶ f (n + 1)) (hf‚ÇÇ : Multipliable fun n : ‚Ñï ‚Ü¶ f (-(n + 1))) :
    ‚àè' n : ‚Ñ§, f n = (‚àè' n : ‚Ñï, f (n + 1)) * f 0 * ‚àè' n : ‚Ñï, f (-(n + 1)) :=
  (hf‚ÇÅ.hasProd.of_add_one_of_neg_add_one hf‚ÇÇ.hasProd).tprod_eq

end ContinuousMul

end Monoid

section IsTopologicalGroup

variable [TopologicalSpace G] [IsTopologicalGroup G]

@[to_additive]
lemma HasProd.of_nat_of_neg {f : ‚Ñ§ ‚Üí G} (hf‚ÇÅ : HasProd (fun n : ‚Ñï ‚Ü¶ f n) g)
    (hf‚ÇÇ : HasProd (fun n : ‚Ñï ‚Ü¶ f (-n)) g') : HasProd f (g * g' / f 0) := by
  refine mul_div_assoc' g .. ‚ñ∏ hf‚ÇÅ.of_nat_of_neg_add_one (m' := g' / f 0) ?_
  rwa [‚Üê hasProd_nat_add_iff' 1, prod_range_one, Nat.cast_zero, neg_zero] at hf‚ÇÇ

@[to_additive]
lemma Multipliable.of_nat_of_neg {f : ‚Ñ§ ‚Üí G} (hf‚ÇÅ : Multipliable fun n : ‚Ñï ‚Ü¶ f n)
    (hf‚ÇÇ : Multipliable fun n : ‚Ñï ‚Ü¶ f (-n)) : Multipliable f :=
  (hf‚ÇÅ.hasProd.of_nat_of_neg hf‚ÇÇ.hasProd).multipliable

@[to_additive]
protected lemma Multipliable.tprod_of_nat_of_neg [T2Space G] {f : ‚Ñ§ ‚Üí G}
    (hf‚ÇÅ : Multipliable fun n : ‚Ñï ‚Ü¶ f n) (hf‚ÇÇ : Multipliable fun n : ‚Ñï ‚Ü¶ f (-n)) :
    ‚àè' n : ‚Ñ§, f n = (‚àè' n : ‚Ñï, f n) * (‚àè' n : ‚Ñï, f (-n)) / f 0 :=
  (hf‚ÇÅ.hasProd.of_nat_of_neg hf‚ÇÇ.hasProd).tprod_eq

@[deprecated (since := "2025-04-12")] alias tsum_of_nat_of_neg :=
  Summable.tsum_of_nat_of_neg
@[to_additive existing, deprecated (since := "2025-04-12")] alias tprod_of_nat_of_neg :=
  Multipliable.tprod_of_nat_of_neg

end IsTopologicalGroup

section IsUniformGroup -- results which depend on completeness

variable [UniformSpace G] [IsUniformGroup G] [CompleteSpace G]

/-- "iff" version of `Multipliable.of_nat_of_neg_add_one`. -/
@[to_additive /-- "iff" version of `Summable.of_nat_of_neg_add_one`. -/]
lemma multipliable_int_iff_multipliable_nat_and_neg_add_one {f : ‚Ñ§ ‚Üí G} : Multipliable f ‚Üî
    (Multipliable fun n : ‚Ñï ‚Ü¶ f n) ‚àß (Multipliable fun n : ‚Ñï ‚Ü¶ f (-(n + 1))) := by
  refine ‚ü®fun p ‚Ü¶ ‚ü®?_, ?_‚ü©, fun ‚ü®hf‚ÇÅ, hf‚ÇÇ‚ü© ‚Ü¶ Multipliable.of_nat_of_neg_add_one hf‚ÇÅ hf‚ÇÇ‚ü© <;>
  apply p.comp_injective
  exacts [Nat.cast_injective, @Int.negSucc.inj]

/-- "iff" version of `Multipliable.of_nat_of_neg`. -/
@[to_additive /-- "iff" version of `Summable.of_nat_of_neg`. -/]
lemma multipliable_int_iff_multipliable_nat_and_neg {f : ‚Ñ§ ‚Üí G} :
    Multipliable f ‚Üî (Multipliable fun n : ‚Ñï ‚Ü¶ f n) ‚àß (Multipliable fun n : ‚Ñï ‚Ü¶ f (-n)) := by
  refine ‚ü®fun p ‚Ü¶ ‚ü®?_, ?_‚ü©, fun ‚ü®hf‚ÇÅ, hf‚ÇÇ‚ü© ‚Ü¶ Multipliable.of_nat_of_neg hf‚ÇÅ hf‚ÇÇ‚ü© <;>
  apply p.comp_injective
  exacts [Nat.cast_injective, neg_injective.comp Nat.cast_injective]

-- We're not really using the ring structure here:
-- we only use multiplication by `-1`, so perhaps this can be generalised further.
theorem Summable.alternating {Œ±} [Ring Œ±]
    [UniformSpace Œ±] [IsUniformAddGroup Œ±] [CompleteSpace Œ±] {f : ‚Ñï ‚Üí Œ±} (hf : Summable f) :
    Summable (fun n => (-1) ^ n * f n) := by
  apply Summable.even_add_odd
  ¬∑ simp only [even_two, Even.mul_right, Even.neg_pow, one_pow, one_mul]
    exact hf.comp_injective (mul_right_injective‚ÇÄ (two_ne_zero' ‚Ñï))
  ¬∑ simp only [pow_add, even_two, Even.mul_right, Even.neg_pow, one_pow, pow_one, mul_neg, mul_one,
      neg_mul, one_mul]
    apply Summable.neg
    apply hf.comp_injective
    exact (add_left_injective 1).comp (mul_right_injective‚ÇÄ (two_ne_zero' ‚Ñï))

end IsUniformGroup

end Int

section pnat

@[to_additive]
theorem pnat_multipliable_iff_multipliable_succ {Œ± : Type*} [TopologicalSpace Œ±] [CommMonoid Œ±]
    {f : ‚Ñï ‚Üí Œ±} : Multipliable (fun x : ‚Ñï+ => f x) ‚Üî Multipliable fun x : ‚Ñï => f (x + 1) :=
  Equiv.pnatEquivNat.symm.multipliable_iff.symm

@[to_additive]
theorem tprod_pnat_eq_tprod_succ {Œ± : Type*} [TopologicalSpace Œ±] [CommMonoid Œ±] (f : ‚Ñï ‚Üí Œ±) :
    ‚àè' n : ‚Ñï+, f n = ‚àè' n, f (n + 1) := (Equiv.pnatEquivNat.symm.tprod_eq _).symm

end pnat
