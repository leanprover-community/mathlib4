/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl
-/

import Mathlib.Topology.Algebra.InfiniteSum.Group
import Mathlib.Logic.Encodable.Lattice

/-!
# Infinite sums over `â„•` and `â„¤`

This file contains lemmas about `HasSum`, `Summable`, and `tsum` applied to the important special
case where the domain is `â„•` or `â„¤`. For instance, we prove the formula
`âˆ‘ i in range k, f i + âˆ‘' i, f (i + k) = âˆ‘' i, f i`, in `sum_add_tsum_nat_add`, as well as several
results relating sums on `â„•` and `â„¤`.
-/

noncomputable section

open Filter Finset Function Encodable

open scoped BigOperators Topology

variable {Î± Î² Î³ Î´ : Type*}

section Monoid

variable [AddCommMonoid Î±] [TopologicalSpace Î±]

section HasSum

variable {a b : Î±}

/-- If `f : â„• â†’ Î±` has sum `a`, then the partial sums `âˆ‘_{i=0}^{n-1} f i` converge to `a`. -/
theorem HasSum.tendsto_sum_nat {f : â„• â†’ Î±} (h : HasSum f a) :
    Tendsto (fun n : â„• â†¦ âˆ‘ i in range n, f i) atTop (ğ“ a) :=
  h.comp tendsto_finset_range
#align has_sum.tendsto_sum_nat HasSum.tendsto_sum_nat

theorem Summable.hasSum_iff_tendsto_nat [T2Space Î±] {f : â„• â†’ Î±} {a : Î±} (hf : Summable f) :
    HasSum f a â†” Tendsto (fun n : â„• â†¦ âˆ‘ i in range n, f i) atTop (ğ“ a) := by
  refine âŸ¨fun h â†¦ h.tendsto_sum_nat, fun h â†¦ ?_âŸ©
  rw [tendsto_nhds_unique h hf.hasSum.tendsto_sum_nat]
  exact hf.hasSum
#align summable.has_sum_iff_tendsto_nat Summable.hasSum_iff_tendsto_nat

section ContinuousAdd

variable [ContinuousAdd Î±]

theorem HasSum.sum_range_add {f : â„• â†’ Î±}
    {k : â„•} {a : Î±} (h : HasSum (fun n â†¦ f (n + k)) a) : HasSum f ((âˆ‘ i in range k, f i) + a) := by
  refine ((range k).hasSum f).add_compl ?_
  rwa [â† (notMemRangeEquiv k).symm.hasSum_iff]

theorem HasSum.even_add_odd {f : â„• â†’ Î±} (he : HasSum (fun k â†¦ f (2 * k)) a)
    (ho : HasSum (fun k â†¦ f (2 * k + 1)) b) : HasSum f (a + b) := by
  have := mul_right_injectiveâ‚€ (two_ne_zero' â„•)
  replace he := this.hasSum_range_iff.2 he
  replace ho := ((add_left_injective 1).comp this).hasSum_range_iff.2 ho
  refine' he.add_isCompl _ ho
  simpa [(Â· âˆ˜ Â·)] using Nat.isCompl_even_odd
#align has_sum.even_add_odd HasSum.even_add_odd

theorem HasSum.sum_nat_of_sum_int {f : â„¤ â†’ Î±} (hf : HasSum f a) :
    HasSum (fun n : â„• â†¦ f n + f (-n)) (a + f 0) := by
  apply (hf.add (hasSum_ite_eq (0 : â„¤) (f 0))).hasSum_of_sum_eq fun u â†¦ ?_
  refine' âŸ¨u.image Int.natAbs, fun v' hv' â†¦ _âŸ©
  let u1 := v'.image fun x : â„• â†¦ (x : â„¤)
  let u2 := v'.image fun x : â„• â†¦ -(x : â„¤)
  have A : u âŠ† u1 âˆª u2 := by
    intro x hx
    simp only [u1, u2, mem_union, mem_image, exists_prop]
    rcases le_total 0 x with (h'x | h'x)
    Â· left
      refine' âŸ¨Int.natAbs x, hv' _, _âŸ©
      Â· simp only [mem_image, exists_prop]
        exact âŸ¨x, hx, rflâŸ©
      Â· simp only [h'x, Int.coe_natAbs, abs_eq_self]
    Â· right
      refine' âŸ¨Int.natAbs x, hv' _, _âŸ©
      Â· simp only [mem_image, exists_prop]
        exact âŸ¨x, hx, rflâŸ©
      Â· simp only [abs_of_nonpos h'x, Int.coe_natAbs, neg_neg]
  refine' âŸ¨u1 âˆª u2, A, _âŸ©
  calc
    (âˆ‘ x in u1 âˆª u2, (f x + ite (x = 0) (f 0) 0)) =
        (âˆ‘ x in u1 âˆª u2, f x) + âˆ‘ x in u1 âˆ© u2, f x := by
      rw [sum_add_distrib]
      congr 1
      refine' (sum_subset_zero_on_sdiff inter_subset_union _ _).symm
      Â· intro x hx
        suffices x â‰  0 by simp only [this, if_false]
        rintro rfl
        simp [u1, u2] at hx
      Â· intro x hx
        simp only [u1, u2, mem_inter, mem_image, exists_prop] at hx
        have : x = 0 := by
          apply le_antisymm
          Â· rcases hx.2 with âŸ¨a, _, rflâŸ©
            simp only [Right.neg_nonpos_iff, Nat.cast_nonneg]
          Â· rcases hx.1 with âŸ¨a, _, rflâŸ©
            simp only [Nat.cast_nonneg]
        simp only [this, eq_self_iff_true, if_true]
    _ = (âˆ‘ x in u1, f x) + âˆ‘ x in u2, f x := sum_union_inter
    _ = (âˆ‘ b in v', f b) + âˆ‘ b in v', f (-b) := by simp [u1, u2]
    _ = âˆ‘ b in v', (f b + f (-b)) := sum_add_distrib.symm
#align has_sum.sum_nat_of_sum_int HasSum.sum_nat_of_sum_int

end ContinuousAdd

end HasSum

section tsum

variable [T2Space Î±] {f g : Î² â†’ Î±} {a aâ‚ aâ‚‚ : Î±}

section Encodable

variable [Encodable Î³]

/-- You can compute a sum over an encodable type by summing over the natural numbers and
  taking a supremum. This is useful for outer measures. -/
theorem tsum_iSup_decodeâ‚‚ [CompleteLattice Î²] (m : Î² â†’ Î±) (m0 : m âŠ¥ = 0) (s : Î³ â†’ Î²) :
    âˆ‘' i : â„•, m (â¨† b âˆˆ decodeâ‚‚ Î³ i, s b) = âˆ‘' b : Î³, m (s b) := by
  rw [â† tsum_extend_zero (@encode_injective Î³ _)]
  refine tsum_congr fun n â†¦ ?_
  rcases em (n âˆˆ Set.range (encode : Î³ â†’ â„•)) with âŸ¨a, rflâŸ© | hn
  Â· simp [encode_injective.extend_apply]
  Â· rw [extend_apply' _ _ _ hn]
    rw [â† decodeâ‚‚_ne_none_iff, ne_eq, not_not] at hn
    simp [hn, m0]
#align tsum_supr_decodeâ‚‚ tsum_iSup_decodeâ‚‚

/-- `tsum_iSup_decodeâ‚‚` specialized to the complete lattice of sets. -/
theorem tsum_iUnion_decodeâ‚‚ (m : Set Î² â†’ Î±) (m0 : m âˆ… = 0) (s : Î³ â†’ Set Î²) :
    âˆ‘' i, m (â‹ƒ b âˆˆ decodeâ‚‚ Î³ i, s b) = âˆ‘' b, m (s b) :=
  tsum_iSup_decodeâ‚‚ m m0 s
#align tsum_Union_decodeâ‚‚ tsum_iUnion_decodeâ‚‚

end Encodable

/-! Some properties about measure-like functions. These could also be functions defined on complete
  sublattices of sets, with the property that they are countably sub-additive.
  `R` will probably be instantiated with `(â‰¤)` in all applications.
-/
section Countable

variable [Countable Î³]

/-- If a function is countably sub-additive then it is sub-additive on countable types -/
theorem rel_iSup_tsum [CompleteLattice Î²] (m : Î² â†’ Î±) (m0 : m âŠ¥ = 0) (R : Î± â†’ Î± â†’ Prop)
    (m_iSup : âˆ€ s : â„• â†’ Î², R (m (â¨† i, s i)) (âˆ‘' i, m (s i))) (s : Î³ â†’ Î²) :
    R (m (â¨† b : Î³, s b)) (âˆ‘' b : Î³, m (s b)) := by
  cases nonempty_encodable Î³
  rw [â† iSup_decodeâ‚‚, â† tsum_iSup_decodeâ‚‚ _ m0 s]
  exact m_iSup _
#align rel_supr_tsum rel_iSup_tsum

/-- If a function is countably sub-additive then it is sub-additive on finite sets -/
theorem rel_iSup_sum [CompleteLattice Î²] (m : Î² â†’ Î±) (m0 : m âŠ¥ = 0) (R : Î± â†’ Î± â†’ Prop)
    (m_iSup : âˆ€ s : â„• â†’ Î², R (m (â¨† i, s i)) (âˆ‘' i, m (s i))) (s : Î´ â†’ Î²) (t : Finset Î´) :
    R (m (â¨† d âˆˆ t, s d)) (âˆ‘ d in t, m (s d)) := by
  rw [iSup_subtype', â† Finset.tsum_subtype]
  exact rel_iSup_tsum m m0 R m_iSup _
#align rel_supr_sum rel_iSup_sum

/-- If a function is countably sub-additive then it is binary sub-additive -/
theorem rel_sup_add [CompleteLattice Î²] (m : Î² â†’ Î±) (m0 : m âŠ¥ = 0) (R : Î± â†’ Î± â†’ Prop)
    (m_iSup : âˆ€ s : â„• â†’ Î², R (m (â¨† i, s i)) (âˆ‘' i, m (s i))) (sâ‚ sâ‚‚ : Î²) :
    R (m (sâ‚ âŠ” sâ‚‚)) (m sâ‚ + m sâ‚‚) := by
  convert rel_iSup_tsum m m0 R m_iSup fun b â†¦ cond b sâ‚ sâ‚‚
  Â· simp only [iSup_bool_eq, cond]
  Â· rw [tsum_fintype, Fintype.sum_bool, cond, cond]
#align rel_sup_add rel_sup_add

end Countable

section ContinuousAdd

variable [ContinuousAdd Î±]

theorem sum_add_tsum_nat_add'
    {f : â„• â†’ Î±} {k : â„•} (h : Summable (fun n â†¦ f (n + k))) :
    ((âˆ‘ i in range k, f i) + âˆ‘' i, f (i + k)) = âˆ‘' i, f i :=
  h.hasSum.sum_range_add.tsum_eq.symm

theorem tsum_eq_zero_add'
    {f : â„• â†’ Î±} (hf : Summable (fun n â†¦ f (n + 1))) :
    âˆ‘' b, f b = f 0 + âˆ‘' b, f (b + 1) := by
  simpa only [sum_range_one] using (sum_add_tsum_nat_add' hf).symm

theorem tsum_even_add_odd {f : â„• â†’ Î±} (he : Summable fun k â†¦ f (2 * k))
    (ho : Summable fun k â†¦ f (2 * k + 1)) :
    âˆ‘' k, f (2 * k) + âˆ‘' k, f (2 * k + 1) = âˆ‘' k, f k :=
  (he.hasSum.even_add_odd ho.hasSum).tsum_eq.symm
#align tsum_even_add_odd tsum_even_add_odd

end ContinuousAdd

end tsum

end Monoid

section TopologicalGroup

variable [AddCommGroup Î±] [TopologicalSpace Î±] [TopologicalAddGroup Î±]

section Nat

theorem hasSum_nat_add_iff {f : â„• â†’ Î±} (k : â„•) {a : Î±} :
    HasSum (fun n â†¦ f (n + k)) a â†” HasSum f (a + âˆ‘ i in range k, f i) := by
  refine' Iff.trans _ (range k).hasSum_compl_iff
  rw [â† (notMemRangeEquiv k).symm.hasSum_iff]
  rfl
#align has_sum_nat_add_iff hasSum_nat_add_iff

theorem summable_nat_add_iff {f : â„• â†’ Î±} (k : â„•) : (Summable fun n â†¦ f (n + k)) â†” Summable f :=
  Iff.symm <|
    (Equiv.addRight (âˆ‘ i in range k, f i)).surjective.summable_iff_of_hasSum_iff
      (hasSum_nat_add_iff k).symm
#align summable_nat_add_iff summable_nat_add_iff

theorem hasSum_nat_add_iff' {f : â„• â†’ Î±} (k : â„•) {a : Î±} :
    HasSum (fun n â†¦ f (n + k)) (a - âˆ‘ i in range k, f i) â†” HasSum f a := by
  simp [hasSum_nat_add_iff]
#align has_sum_nat_add_iff' hasSum_nat_add_iff'

theorem sum_add_tsum_nat_add [T2Space Î±] {f : â„• â†’ Î±} (k : â„•) (h : Summable f) :
    ((âˆ‘ i in range k, f i) + âˆ‘' i, f (i + k)) = âˆ‘' i, f i :=
  sum_add_tsum_nat_add' <| (summable_nat_add_iff k).2 h
#align sum_add_tsum_nat_add sum_add_tsum_nat_add

theorem tsum_eq_zero_add [T2Space Î±] {f : â„• â†’ Î±} (hf : Summable f) :
    âˆ‘' b, f b = f 0 + âˆ‘' b, f (b + 1) :=
  tsum_eq_zero_add' <| (summable_nat_add_iff 1).2 hf
#align tsum_eq_zero_add tsum_eq_zero_add

/-- For `f : â„• â†’ Î±`, then `âˆ‘' k, f (k + i)` tends to zero. This does not require a summability
assumption on `f`, as otherwise all sums are zero. -/
theorem tendsto_sum_nat_add [T2Space Î±] (f : â„• â†’ Î±) :
    Tendsto (fun i â†¦ âˆ‘' k, f (k + i)) atTop (ğ“ 0) := by
  by_cases hf : Summable f
  Â· have hâ‚€ : (fun i â†¦ âˆ‘' i, f i - âˆ‘ j in range i, f j) = fun i â†¦ âˆ‘' k : â„•, f (k + i) := by
      ext1 i
      rw [sub_eq_iff_eq_add, add_comm, sum_add_tsum_nat_add i hf]
    have hâ‚ : Tendsto (fun _ : â„• â†¦ âˆ‘' i, f i) atTop (ğ“ (âˆ‘' i, f i)) := tendsto_const_nhds
    simpa only [hâ‚€, sub_self] using Tendsto.sub hâ‚ hf.hasSum.tendsto_sum_nat
  Â· refine tendsto_const_nhds.congr fun n â†¦ (tsum_eq_zero_of_not_summable ?_).symm
    rwa [summable_nat_add_iff n]
#align tendsto_sum_nat_add tendsto_sum_nat_add

/-- If `fâ‚€, fâ‚, fâ‚‚, ...` and `gâ‚€, gâ‚, gâ‚‚, ...` are both convergent then so is the `â„¤`-indexed
sequence: `..., gâ‚‚, gâ‚, gâ‚€, fâ‚€, fâ‚, fâ‚‚, ...`. -/
theorem HasSum.int_rec {a b : Î±} {f g : â„• â†’ Î±} (hf : HasSum f a) (hg : HasSum g b) :
    @HasSum Î± _ _ _ (@Int.rec (fun _ â†¦ Î±) f g : â„¤ â†’ Î±) (a + b) := by
  -- note this proof works for any two-case inductive
  have hâ‚ : Injective ((â†‘) : â„• â†’ â„¤) := @Int.ofNat.inj
  have hâ‚‚ : Injective Int.negSucc := @Int.negSucc.inj
  have : IsCompl (Set.range ((â†‘) : â„• â†’ â„¤)) (Set.range Int.negSucc) := by
    constructor
    Â· rw [disjoint_iff_inf_le]
      rintro _ âŸ¨âŸ¨i, rflâŸ©, âŸ¨j, âŸ¨âŸ©âŸ©âŸ©
    Â· rw [codisjoint_iff_le_sup]
      rintro (i | j) _
      exacts [Or.inl âŸ¨_, rflâŸ©, Or.inr âŸ¨_, rflâŸ©]
  exact HasSum.add_isCompl this (hâ‚.hasSum_range_iff.mpr hf) (hâ‚‚.hasSum_range_iff.mpr hg)
#align has_sum.int_rec HasSum.int_rec

theorem HasSum.nonneg_add_neg {a b : Î±} {f : â„¤ â†’ Î±} (hnonneg : HasSum (fun n : â„• â†¦ f n) a)
    (hneg : HasSum (fun n : â„• â†¦ f (-n.succ)) b) : HasSum f (a + b) := by
  simp_rw [â† Int.negSucc_coe] at hneg
  convert hnonneg.int_rec hneg using 1
  ext (i | j) <;> rfl
#align has_sum.nonneg_add_neg HasSum.nonneg_add_neg

theorem HasSum.pos_add_zero_add_neg {a b : Î±} {f : â„¤ â†’ Î±} (hpos : HasSum (fun n : â„• â†¦ f (n + 1)) a)
    (hneg : HasSum (fun n : â„• â†¦ f (-n.succ)) b) : HasSum f (a + f 0 + b) :=
  haveI : âˆ€ g : â„• â†’ Î±, HasSum (fun k â†¦ g (k + 1)) a â†’ HasSum g (a + g 0) := by
    intro g hg
    simpa using (hasSum_nat_add_iff _).mp hg
  (this (fun n â†¦ f n) hpos).nonneg_add_neg hneg
#align has_sum.pos_add_zero_add_neg HasSum.pos_add_zero_add_neg

theorem summable_int_of_summable_nat {f : â„¤ â†’ Î±} (hp : Summable fun n : â„• â†¦ f n)
    (hn : Summable fun n : â„• â†¦ f (-n)) : Summable f :=
  (HasSum.nonneg_add_neg hp.hasSum <| Summable.hasSum <| (summable_nat_add_iff 1).mpr hn).summable
#align summable_int_of_summable_nat summable_int_of_summable_nat

end Nat

end TopologicalGroup

section UniformGroup

variable [AddCommGroup Î±] [UniformSpace Î±]

variable [UniformAddGroup Î±] {f g : Î² â†’ Î±} {a aâ‚ aâ‚‚ : Î±}

theorem cauchySeq_finset_iff_nat_tsum_vanishing {f : â„• â†’ Î±} :
    (CauchySeq fun s : Finset â„• â†¦ âˆ‘ n in s, f n) â†”
      âˆ€ e âˆˆ ğ“ (0 : Î±), âˆƒ N : â„•, âˆ€ t âŠ† {n | N â‰¤ n}, (âˆ‘' n : t, f n) âˆˆ e := by
  refine cauchySeq_finset_iff_tsum_vanishing.trans âŸ¨fun vanish e he â†¦ ?_, fun vanish e he â†¦ ?_âŸ©
  Â· obtain âŸ¨s, hsâŸ© := vanish e he
    refine âŸ¨if h : s.Nonempty then s.max' h + 1 else 0, fun t ht â†¦ hs _ <| Set.disjoint_left.mpr ?_âŸ©
    split_ifs at ht with h
    Â· exact fun m hmt hms â†¦ (s.le_max' _ hms).not_lt (Nat.succ_le_iff.mp <| ht hmt)
    Â· exact fun _ _ hs â†¦ h âŸ¨_, hsâŸ©
  Â· obtain âŸ¨N, hNâŸ© := vanish e he
    exact âŸ¨range N, fun t ht â†¦ hN _ fun n hnt â†¦
      le_of_not_lt fun h â†¦ Set.disjoint_left.mp ht hnt (mem_range.mpr h)âŸ©

variable [CompleteSpace Î±]

theorem summable_iff_nat_tsum_vanishing {f : â„• â†’ Î±} : Summable f â†”
    âˆ€ e âˆˆ ğ“ (0 : Î±), âˆƒ N : â„•, âˆ€ t âŠ† {n | N â‰¤ n}, (âˆ‘' n : t, f n) âˆˆ e := by
  rw [summable_iff_cauchySeq_finset, cauchySeq_finset_iff_nat_tsum_vanishing]

end UniformGroup

section TopologicalGroup

variable {G : Type*} [TopologicalSpace G] [AddCommGroup G] [TopologicalAddGroup G] {f : Î± â†’ G}

theorem Summable.nat_tsum_vanishing {f : â„• â†’ G} (hf : Summable f) â¦ƒe : Set Gâ¦„ (he : e âˆˆ ğ“ 0) :
    âˆƒ N : â„•, âˆ€ t âŠ† {n | N â‰¤ n}, (âˆ‘' n : t, f n) âˆˆ e :=
  letI : UniformSpace G := TopologicalAddGroup.toUniformSpace G
  have : UniformAddGroup G := comm_topologicalAddGroup_is_uniform
  cauchySeq_finset_iff_nat_tsum_vanishing.1 hf.hasSum.cauchySeq e he

theorem Summable.tendsto_atTop_zero {f : â„• â†’ G} (hf : Summable f) : Tendsto f atTop (ğ“ 0) := by
  rw [â† Nat.cofinite_eq_atTop]
  exact hf.tendsto_cofinite_zero
#align summable.tendsto_at_top_zero Summable.tendsto_atTop_zero

end TopologicalGroup
