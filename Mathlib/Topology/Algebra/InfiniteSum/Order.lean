/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl
-/
import Mathlib.Algebra.Order.Archimedean
import Mathlib.Topology.Algebra.InfiniteSum.Basic
import Mathlib.Topology.Algebra.Order.Field
import Mathlib.Topology.Algebra.Order.MonotoneConvergence

#align_import topology.algebra.infinite_sum.order from "leanprover-community/mathlib"@"32253a1a1071173b33dc7d6a218cf722c6feb514"

/-!
# Infinite sum in an order

This file provides lemmas about the interaction of infinite sums and order operations.
-/


open Finset Filter Function BigOperators
open scoped Classical

variable {Î¹ Îº Î± : Type*}

section Preorder

variable [Preorder Î±] [AddCommMonoid Î±] [TopologicalSpace Î±] [OrderClosedTopology Î±] [T2Space Î±]
  {f : â„• â†’ Î±} {c : Î±}

theorem tsum_le_of_sum_range_le (hf : Summable f) (h : âˆ€ n, âˆ‘ i in range n, f i â‰¤ c) :
    âˆ‘' n, f n â‰¤ c :=
  let âŸ¨_l, hlâŸ© := hf
  hl.tsum_eq.symm â–¸ le_of_tendsto' hl.tendsto_sum_nat h
#align tsum_le_of_sum_range_le tsum_le_of_sum_range_le

end Preorder

section OrderedAddCommMonoid

variable [OrderedAddCommMonoid Î±] [TopologicalSpace Î±] [OrderClosedTopology Î±] {f g : Î¹ â†’ Î±}
  {a aâ‚ aâ‚‚ : Î±}

theorem hasSum_le (h : âˆ€ i, f i â‰¤ g i) (hf : HasSum f aâ‚) (hg : HasSum g aâ‚‚) : aâ‚ â‰¤ aâ‚‚ :=
  le_of_tendsto_of_tendsto' hf hg fun _ => sum_le_sum fun i _ => h i
#align has_sum_le hasSum_le

@[mono]
theorem hasSum_mono (hf : HasSum f aâ‚) (hg : HasSum g aâ‚‚) (h : f â‰¤ g) : aâ‚ â‰¤ aâ‚‚ :=
  hasSum_le h hf hg
#align has_sum_mono hasSum_mono

theorem hasSum_le_of_sum_le (hf : HasSum f a) (h : âˆ€ s, âˆ‘ i in s, f i â‰¤ aâ‚‚) : a â‰¤ aâ‚‚ :=
  le_of_tendsto' hf h
#align has_sum_le_of_sum_le hasSum_le_of_sum_le

theorem le_hasSum_of_le_sum (hf : HasSum f a) (h : âˆ€ s, aâ‚‚ â‰¤ âˆ‘ i in s, f i) : aâ‚‚ â‰¤ a :=
  ge_of_tendsto' hf h
#align le_has_sum_of_le_sum le_hasSum_of_le_sum

theorem hasSum_le_inj {g : Îº â†’ Î±} (e : Î¹ â†’ Îº) (he : Injective e)
    (hs : âˆ€ c, c âˆ‰ Set.range e â†’ 0 â‰¤ g c) (h : âˆ€ i, f i â‰¤ g (e i)) (hf : HasSum f aâ‚)
    (hg : HasSum g aâ‚‚) : aâ‚ â‰¤ aâ‚‚ := by
  rw [â† hasSum_extend_zero he] at hf
  -- âŠ¢ aâ‚ â‰¤ aâ‚‚
  refine hasSum_le (fun c => ?_) hf hg
  -- âŠ¢ extend e f 0 c â‰¤ g c
  obtain âŸ¨i, rflâŸ© | h := em (c âˆˆ Set.range e)
  -- âŠ¢ extend e f 0 (e i) â‰¤ g (e i)
  Â· rw [he.extend_apply]
    -- âŠ¢ f i â‰¤ g (e i)
    exact h _
    -- ğŸ‰ no goals
  Â· rw [extend_apply' _ _ _ h]
    -- âŠ¢ OfNat.ofNat 0 c â‰¤ g c
    exact hs _ h
    -- ğŸ‰ no goals
#align has_sum_le_inj hasSum_le_inj

theorem tsum_le_tsum_of_inj {g : Îº â†’ Î±} (e : Î¹ â†’ Îº) (he : Injective e)
    (hs : âˆ€ c, c âˆ‰ Set.range e â†’ 0 â‰¤ g c) (h : âˆ€ i, f i â‰¤ g (e i)) (hf : Summable f)
    (hg : Summable g) : tsum f â‰¤ tsum g :=
  hasSum_le_inj _ he hs h hf.hasSum hg.hasSum
#align tsum_le_tsum_of_inj tsum_le_tsum_of_inj

theorem sum_le_hasSum (s : Finset Î¹) (hs : âˆ€ i, i âˆ‰ s â†’ 0 â‰¤ f i) (hf : HasSum f a) :
    âˆ‘ i in s, f i â‰¤ a :=
  ge_of_tendsto hf (eventually_atTop.2
    âŸ¨s, fun _t hst => sum_le_sum_of_subset_of_nonneg hst fun i _ hbs => hs i hbsâŸ©)
#align sum_le_has_sum sum_le_hasSum

theorem isLUB_hasSum (h : âˆ€ i, 0 â‰¤ f i) (hf : HasSum f a) :
    IsLUB (Set.range fun s => âˆ‘ i in s, f i) a :=
  isLUB_of_tendsto_atTop (Finset.sum_mono_set_of_nonneg h) hf
#align is_lub_has_sum isLUB_hasSum

theorem le_hasSum (hf : HasSum f a) (i : Î¹) (hb : âˆ€ j, j â‰  i â†’ 0 â‰¤ f j) : f i â‰¤ a :=
  calc
    f i = âˆ‘ i in {i}, f i := Finset.sum_singleton.symm
    _ â‰¤ a := sum_le_hasSum _ (by simpa) hf
                                 -- ğŸ‰ no goals
#align le_has_sum le_hasSum

theorem sum_le_tsum {f : Î¹ â†’ Î±} (s : Finset Î¹) (hs : âˆ€ i, i âˆ‰ s â†’ 0 â‰¤ f i) (hf : Summable f) :
    âˆ‘ i in s, f i â‰¤ âˆ‘' i, f i :=
  sum_le_hasSum s hs hf.hasSum
#align sum_le_tsum sum_le_tsum

theorem le_tsum (hf : Summable f) (i : Î¹) (hb : âˆ€ j, j â‰  i â†’ 0 â‰¤ f j) : f i â‰¤ âˆ‘' i, f i :=
  le_hasSum hf.hasSum i hb
#align le_tsum le_tsum

theorem tsum_le_tsum (h : âˆ€ i, f i â‰¤ g i) (hf : Summable f) (hg : Summable g) :
    âˆ‘' i, f i â‰¤ âˆ‘' i, g i :=
  hasSum_le h hf.hasSum hg.hasSum
#align tsum_le_tsum tsum_le_tsum

@[mono]
theorem tsum_mono (hf : Summable f) (hg : Summable g) (h : f â‰¤ g) : âˆ‘' n, f n â‰¤ âˆ‘' n, g n :=
  tsum_le_tsum h hf hg
#align tsum_mono tsum_mono

theorem tsum_le_of_sum_le (hf : Summable f) (h : âˆ€ s, âˆ‘ i in s, f i â‰¤ aâ‚‚) : âˆ‘' i, f i â‰¤ aâ‚‚ :=
  hasSum_le_of_sum_le hf.hasSum h
#align tsum_le_of_sum_le tsum_le_of_sum_le

theorem tsum_le_of_sum_le' (haâ‚‚ : 0 â‰¤ aâ‚‚) (h : âˆ€ s, âˆ‘ i in s, f i â‰¤ aâ‚‚) : âˆ‘' i, f i â‰¤ aâ‚‚ := by
  by_cases hf : Summable f
  -- âŠ¢ âˆ‘' (i : Î¹), f i â‰¤ aâ‚‚
  Â· exact tsum_le_of_sum_le hf h
    -- ğŸ‰ no goals
  Â· rw [tsum_eq_zero_of_not_summable hf]
    -- âŠ¢ 0 â‰¤ aâ‚‚
    exact haâ‚‚
    -- ğŸ‰ no goals
#align tsum_le_of_sum_le' tsum_le_of_sum_le'

theorem HasSum.nonneg (h : âˆ€ i, 0 â‰¤ g i) (ha : HasSum g a) : 0 â‰¤ a :=
  hasSum_le h hasSum_zero ha
#align has_sum.nonneg HasSum.nonneg

theorem HasSum.nonpos (h : âˆ€ i, g i â‰¤ 0) (ha : HasSum g a) : a â‰¤ 0 :=
  hasSum_le h ha hasSum_zero
#align has_sum.nonpos HasSum.nonpos

theorem tsum_nonneg (h : âˆ€ i, 0 â‰¤ g i) : 0 â‰¤ âˆ‘' i, g i := by
  by_cases hg : Summable g
  -- âŠ¢ 0 â‰¤ âˆ‘' (i : Î¹), g i
  Â· exact hg.hasSum.nonneg h
    -- ğŸ‰ no goals
  Â· rw [tsum_eq_zero_of_not_summable hg]
    -- ğŸ‰ no goals
#align tsum_nonneg tsum_nonneg

theorem tsum_nonpos (h : âˆ€ i, f i â‰¤ 0) : âˆ‘' i, f i â‰¤ 0 := by
  by_cases hf : Summable f
  -- âŠ¢ âˆ‘' (i : Î¹), f i â‰¤ 0
  Â· exact hf.hasSum.nonpos h
    -- ğŸ‰ no goals
  Â· rw [tsum_eq_zero_of_not_summable hf]
    -- ğŸ‰ no goals
#align tsum_nonpos tsum_nonpos

-- porting note: generalized from `OrderedAddCommGroup` to `OrderedAddCommMonoid`
theorem hasSum_zero_iff_of_nonneg (hf : âˆ€ i, 0 â‰¤ f i) : HasSum f 0 â†” f = 0 := by
  refine' âŸ¨fun hf' => _, _âŸ©
  -- âŠ¢ f = 0
  Â· ext i
    -- âŠ¢ f i = OfNat.ofNat 0 i
    exact (hf i).antisymm' (le_hasSum hf' _ fun j _ => hf j)
    -- ğŸ‰ no goals
  Â· rintro rfl
    -- âŠ¢ HasSum 0 0
    exact hasSum_zero
    -- ğŸ‰ no goals
#align has_sum_zero_iff_of_nonneg hasSum_zero_iff_of_nonneg

end OrderedAddCommMonoid

section OrderedAddCommGroup

variable [OrderedAddCommGroup Î±] [TopologicalSpace Î±] [TopologicalAddGroup Î±]
  [OrderClosedTopology Î±] {f g : Î¹ â†’ Î±} {aâ‚ aâ‚‚ : Î±} {i : Î¹}

theorem hasSum_lt (h : f â‰¤ g) (hi : f i < g i) (hf : HasSum f aâ‚) (hg : HasSum g aâ‚‚) : aâ‚ < aâ‚‚ := by
  have : update f i 0 â‰¤ update g i 0 := update_le_update_iff.mpr âŸ¨rfl.le, fun i _ => h iâŸ©
  -- âŠ¢ aâ‚ < aâ‚‚
  have : 0 - f i + aâ‚ â‰¤ 0 - g i + aâ‚‚ := hasSum_le this (hf.update i 0) (hg.update i 0)
  -- âŠ¢ aâ‚ < aâ‚‚
  simpa only [zero_sub, add_neg_cancel_left] using add_lt_add_of_lt_of_le hi this
  -- ğŸ‰ no goals
#align has_sum_lt hasSum_lt

@[mono]
theorem hasSum_strict_mono (hf : HasSum f aâ‚) (hg : HasSum g aâ‚‚) (h : f < g) : aâ‚ < aâ‚‚ :=
  let âŸ¨hle, _i, hiâŸ© := Pi.lt_def.mp h
  hasSum_lt hle hi hf hg
#align has_sum_strict_mono hasSum_strict_mono

theorem tsum_lt_tsum (h : f â‰¤ g) (hi : f i < g i) (hf : Summable f) (hg : Summable g) :
    âˆ‘' n, f n < âˆ‘' n, g n :=
  hasSum_lt h hi hf.hasSum hg.hasSum
#align tsum_lt_tsum tsum_lt_tsum

@[mono]
theorem tsum_strict_mono (hf : Summable f) (hg : Summable g) (h : f < g) :
    âˆ‘' n, f n < âˆ‘' n, g n :=
  let âŸ¨hle, _i, hiâŸ© := Pi.lt_def.mp h
  tsum_lt_tsum hle hi hf hg
#align tsum_strict_mono tsum_strict_mono

theorem tsum_pos (hsum : Summable g) (hg : âˆ€ i, 0 â‰¤ g i) (i : Î¹) (hi : 0 < g i) :
    0 < âˆ‘' i, g i := by
  rw [â† tsum_zero]
  -- âŠ¢ âˆ‘' (x : ?m.47496), 0 < âˆ‘' (i : Î¹), g i
  exact tsum_lt_tsum hg hi summable_zero hsum
  -- ğŸ‰ no goals
#align tsum_pos tsum_pos

end OrderedAddCommGroup

section CanonicallyOrderedAddMonoid

variable [CanonicallyOrderedAddMonoid Î±] [TopologicalSpace Î±] [OrderClosedTopology Î±] {f : Î¹ â†’ Î±}
  {a : Î±}

theorem le_hasSum' (hf : HasSum f a) (i : Î¹) : f i â‰¤ a :=
  le_hasSum hf i fun _ _ => zero_le _
#align le_has_sum' le_hasSum'

theorem le_tsum' (hf : Summable f) (i : Î¹) : f i â‰¤ âˆ‘' i, f i :=
  le_tsum hf i fun _ _ => zero_le _
#align le_tsum' le_tsum'

theorem hasSum_zero_iff : HasSum f 0 â†” âˆ€ x, f x = 0 :=
  (hasSum_zero_iff_of_nonneg fun _ => zero_le _).trans funext_iff
#align has_sum_zero_iff hasSum_zero_iff

theorem tsum_eq_zero_iff (hf : Summable f) : âˆ‘' i, f i = 0 â†” âˆ€ x, f x = 0 := by
  rw [â† hasSum_zero_iff, hf.hasSum_iff]
  -- ğŸ‰ no goals
#align tsum_eq_zero_iff tsum_eq_zero_iff

theorem tsum_ne_zero_iff (hf : Summable f) : âˆ‘' i, f i â‰  0 â†” âˆƒ x, f x â‰  0 := by
  rw [Ne.def, tsum_eq_zero_iff hf, not_forall]
  -- ğŸ‰ no goals
#align tsum_ne_zero_iff tsum_ne_zero_iff

theorem isLUB_hasSum' (hf : HasSum f a) : IsLUB (Set.range fun s => âˆ‘ i in s, f i) a :=
  isLUB_of_tendsto_atTop (Finset.sum_mono_set f) hf
#align is_lub_has_sum' isLUB_hasSum'

end CanonicallyOrderedAddMonoid

section LinearOrder

/-!
For infinite sums taking values in a linearly ordered monoid, the existence of a least upper
bound for the finite sums is a criterion for summability.

This criterion is useful when applied in a linearly ordered monoid which is also a complete or
conditionally complete linear order, such as `â„`, `â„â‰¥0`, `â„â‰¥0âˆ`, because it is then easy to check
the existence of a least upper bound.
-/

theorem hasSum_of_isLUB_of_nonneg [LinearOrderedAddCommMonoid Î±] [TopologicalSpace Î±]
    [OrderTopology Î±] {f : Î¹ â†’ Î±} (i : Î±) (h : âˆ€ i, 0 â‰¤ f i)
    (hf : IsLUB (Set.range fun s => âˆ‘ i in s, f i) i) : HasSum f i :=
  tendsto_atTop_isLUB (Finset.sum_mono_set_of_nonneg h) hf
#align has_sum_of_is_lub_of_nonneg hasSum_of_isLUB_of_nonneg

theorem hasSum_of_isLUB [CanonicallyLinearOrderedAddMonoid Î±] [TopologicalSpace Î±] [OrderTopology Î±]
    {f : Î¹ â†’ Î±} (b : Î±) (hf : IsLUB (Set.range fun s => âˆ‘ i in s, f i) b) : HasSum f b :=
  tendsto_atTop_isLUB (Finset.sum_mono_set f) hf
#align has_sum_of_is_lub hasSum_of_isLUB

theorem summable_abs_iff [LinearOrderedAddCommGroup Î±] [UniformSpace Î±] [UniformAddGroup Î±]
    [CompleteSpace Î±] {f : Î¹ â†’ Î±} : (Summable fun x => |f x|) â†” Summable f :=
  let s := { x | 0 â‰¤ f x }
  have h1 : âˆ€ x : s, |f x| = f x := fun x => abs_of_nonneg x.2
  have h2 : âˆ€ x : â†‘sá¶œ, |f x| = -f x := fun x => abs_of_neg (not_le.1 x.2)
  calc (Summable fun x => |f x|) â†”
      (Summable fun x : s => |f x|) âˆ§ Summable fun x : â†‘sá¶œ => |f x| :=
        summable_subtype_and_compl.symm
  _ â†” (Summable fun x : s => f x) âˆ§ Summable fun x : â†‘sá¶œ => -f x := by simp only [h1, h2]
                                                                       -- ğŸ‰ no goals
  _ â†” Summable f := by simp only [summable_neg_iff, summable_subtype_and_compl]
                       -- ğŸ‰ no goals
#align summable_abs_iff summable_abs_iff

alias âŸ¨Summable.of_abs, Summable.absâŸ© := summable_abs_iff
#align summable.of_abs Summable.of_abs
#align summable.abs Summable.abs

theorem Finite.of_summable_const [LinearOrderedAddCommGroup Î±] [TopologicalSpace Î±] [Archimedean Î±]
    [OrderClosedTopology Î±] {b : Î±} (hb : 0 < b) (hf : Summable fun _ : Î¹ => b) :
    Finite Î¹ := by
  have H : âˆ€ s : Finset Î¹, s.card â€¢ b â‰¤ âˆ‘' _ : Î¹, b := fun s => by
    simpa using sum_le_hasSum s (fun a _ => hb.le) hf.hasSum
  obtain âŸ¨n, hnâŸ© := Archimedean.arch (âˆ‘' _ : Î¹, b) hb
  -- âŠ¢ Finite Î¹
  have : âˆ€ s : Finset Î¹, s.card â‰¤ n := fun s => by
    simpa [nsmul_le_nsmul_iff hb] using (H s).trans hn
  have : Fintype Î¹ := fintypeOfFinsetCardLe n this
  -- âŠ¢ Finite Î¹
  infer_instance
  -- ğŸ‰ no goals

theorem Set.Finite.of_summable_const [LinearOrderedAddCommGroup Î±] [TopologicalSpace Î±]
    [Archimedean Î±] [OrderClosedTopology Î±] {b : Î±} (hb : 0 < b) (hf : Summable fun _ : Î¹ => b) :
    (Set.univ : Set Î¹).Finite :=
  finite_univ_iff.2 <| .of_summable_const hb hf
#align finite_of_summable_const Set.Finite.of_summable_const

end LinearOrder

theorem Summable.tendsto_atTop_of_pos [LinearOrderedField Î±] [TopologicalSpace Î±] [OrderTopology Î±]
    {f : â„• â†’ Î±} (hf : Summable fâ»Â¹) (hf' : âˆ€ n, 0 < f n) : Tendsto f atTop atTop :=
  inv_inv f â–¸ Filter.Tendsto.inv_tendsto_zero <|
    tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within _ hf.tendsto_atTop_zero <|
      eventually_of_forall fun _ => inv_pos.2 (hf' _)
#align summable.tendsto_top_of_pos Summable.tendsto_atTop_of_pos
