/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl
-/
import Mathlib.Data.Nat.Parity
import Mathlib.Logic.Encodable.Lattice
import Mathlib.Topology.Algebra.UniformGroup
import Mathlib.Topology.Algebra.Star

#align_import topology.algebra.infinite_sum.basic from "leanprover-community/mathlib"@"3b52265189f3fb43aa631edffce5d060fafaf82f"

/-!
# Infinite sum over a topological monoid

This sum is known as unconditionally convergent, as it sums to the same value under all possible
permutations. For Euclidean spaces (finite dimensional Banach spaces) this is equivalent to absolute
convergence.

Note: There are summable sequences which are not unconditionally convergent! The other way holds
generally, see `HasSum.tendsto_sum_nat`.

## References

* Bourbaki: General Topology (1995), Chapter 3 Â§5 (Infinite sums in commutative groups)

-/

set_option autoImplicit true


noncomputable section

open Filter Finset Function

open scoped BigOperators Topology

variable {Î± : Type*} {Î² : Type*} {Î³ : Type*} {Î´ : Type*}

section HasSum

variable [AddCommMonoid Î±] [TopologicalSpace Î±]

/-- Infinite sum on a topological monoid

The `atTop` filter on `Finset Î²` is the limit of all finite sets towards the entire type. So we sum
up bigger and bigger sets. This sum operation is invariant under reordering. In particular,
the function `â„• â†’ â„` sending `n` to `(-1)^n / (n+1)` does not have a
sum for this definition, but a series which is absolutely convergent will have the correct sum.

This is based on Mario Carneiro's
[infinite sum `df-tsms` in Metamath](http://us.metamath.org/mpeuni/df-tsms.html).

For the definition or many statements, `Î±` does not need to be a topological monoid. We only add
this assumption later, for the lemmas where it is relevant.
-/
def HasSum (f : Î² â†’ Î±) (a : Î±) : Prop :=
  Tendsto (fun s : Finset Î² => âˆ‘ b in s, f b) atTop (ğ“ a)
#align has_sum HasSum

/-- `Summable f` means that `f` has some (infinite) sum. Use `tsum` to get the value. -/
def Summable (f : Î² â†’ Î±) : Prop :=
  âˆƒ a, HasSum f a
#align summable Summable

open Classical in
/-- `âˆ‘' i, f i` is the sum of `f` it exists, or 0 otherwise. -/
irreducible_def tsum {Î²} (f : Î² â†’ Î±) :=
  if h : Summable f then
  /- Note that the sum might not be uniquely defined if the topology is not separated.
  When the support of `f` is finite, we make the most reasonable choice to use the finite sum over
  the support. Otherwise, we choose arbitrarily an `a` satisfying `HasSum f a`. -/
    if (support f).Finite then finsum f
    else Classical.choose h
  else 0
#align tsum tsum

-- see Note [operator precedence of big operators]
@[inherit_doc tsum]
notation3 "âˆ‘' "(...)", "r:67:(scoped f => tsum f) => r

variable {f g : Î² â†’ Î±} {a b : Î±} {s : Finset Î²}

theorem HasSum.summable (h : HasSum f a) : Summable f :=
  âŸ¨a, hâŸ©
#align has_sum.summable HasSum.summable

/-- Constant zero function has sum `0` -/
theorem hasSum_zero : HasSum (fun _ => 0 : Î² â†’ Î±) 0 := by simp [HasSum, tendsto_const_nhds]
#align has_sum_zero hasSum_zero

theorem hasSum_empty [IsEmpty Î²] : HasSum f 0 := by
  convert @hasSum_zero Î± Î² _ _
#align has_sum_empty hasSum_empty

theorem summable_zero : Summable (fun _ => 0 : Î² â†’ Î±) :=
  hasSum_zero.summable
#align summable_zero summable_zero

theorem summable_empty [IsEmpty Î²] : Summable f :=
  hasSum_empty.summable
#align summable_empty summable_empty

theorem tsum_eq_zero_of_not_summable (h : Â¬Summable f) : âˆ‘' b, f b = 0 := by simp [tsum_def, h]
#align tsum_eq_zero_of_not_summable tsum_eq_zero_of_not_summable

theorem summable_congr (hfg : âˆ€ b, f b = g b) : Summable f â†” Summable g :=
  iff_of_eq (congr_arg Summable <| funext hfg)
#align summable_congr summable_congr

theorem Summable.congr (hf : Summable f) (hfg : âˆ€ b, f b = g b) : Summable g :=
  (summable_congr hfg).mp hf
#align summable.congr Summable.congr

theorem HasSum.hasSum_of_sum_eq {g : Î³ â†’ Î±}
    (h_eq :
      âˆ€ u : Finset Î³,
        âˆƒ v : Finset Î², âˆ€ v', v âŠ† v' â†’ âˆƒ u', u âŠ† u' âˆ§ âˆ‘ x in u', g x = âˆ‘ b in v', f b)
    (hf : HasSum g a) : HasSum f a :=
  le_trans (map_atTop_finset_sum_le_of_sum_eq h_eq) hf
#align has_sum.has_sum_of_sum_eq HasSum.hasSum_of_sum_eq

theorem hasSum_iff_hasSum {g : Î³ â†’ Î±}
    (hâ‚ :
      âˆ€ u : Finset Î³,
        âˆƒ v : Finset Î², âˆ€ v', v âŠ† v' â†’ âˆƒ u', u âŠ† u' âˆ§ âˆ‘ x in u', g x = âˆ‘ b in v', f b)
    (hâ‚‚ :
      âˆ€ v : Finset Î²,
        âˆƒ u : Finset Î³, âˆ€ u', u âŠ† u' â†’ âˆƒ v', v âŠ† v' âˆ§ âˆ‘ b in v', f b = âˆ‘ x in u', g x) :
    HasSum f a â†” HasSum g a :=
  âŸ¨HasSum.hasSum_of_sum_eq hâ‚‚, HasSum.hasSum_of_sum_eq hâ‚âŸ©
#align has_sum_iff_has_sum hasSum_iff_hasSum

theorem Function.Injective.hasSum_iff {g : Î³ â†’ Î²} (hg : Injective g)
    (hf : âˆ€ x, x âˆ‰ Set.range g â†’ f x = 0) : HasSum (f âˆ˜ g) a â†” HasSum f a := by
  simp only [HasSum, Tendsto, comp_apply, hg.map_atTop_finset_sum_eq hf]
#align function.injective.has_sum_iff Function.Injective.hasSum_iff

theorem Function.Injective.summable_iff {g : Î³ â†’ Î²} (hg : Injective g)
    (hf : âˆ€ (x) (_ : x âˆ‰ Set.range g), f x = 0) : Summable (f âˆ˜ g) â†” Summable f :=
  exists_congr fun _ => hg.hasSum_iff hf
#align function.injective.summable_iff Function.Injective.summable_iff

@[simp] theorem hasSum_extend_zero {g : Î² â†’ Î³} (hg : Injective g) :
    HasSum (extend g f 0) a â†” HasSum f a := by
  rw [â† hg.hasSum_iff, extend_comp hg]
  exact extend_apply' _ _

@[simp] theorem summable_extend_zero {g : Î² â†’ Î³} (hg : Injective g) :
    Summable (extend g f 0) â†” Summable f :=
  exists_congr fun _ => hasSum_extend_zero hg

theorem hasSum_subtype_iff_of_support_subset {s : Set Î²} (hf : support f âŠ† s) :
    HasSum (f âˆ˜ (â†‘) : s â†’ Î±) a â†” HasSum f a :=
  Subtype.coe_injective.hasSum_iff <| by simpa using support_subset_iff'.1 hf
#align has_sum_subtype_iff_of_support_subset hasSum_subtype_iff_of_support_subset

theorem hasSum_subtype_iff_indicator {s : Set Î²} :
    HasSum (f âˆ˜ (â†‘) : s â†’ Î±) a â†” HasSum (s.indicator f) a := by
  rw [â† Set.indicator_range_comp, Subtype.range_coe,
    hasSum_subtype_iff_of_support_subset Set.support_indicator_subset]
#align has_sum_subtype_iff_indicator hasSum_subtype_iff_indicator

theorem summable_subtype_iff_indicator {s : Set Î²} :
    Summable (f âˆ˜ (â†‘) : s â†’ Î±) â†” Summable (s.indicator f) :=
  exists_congr fun _ => hasSum_subtype_iff_indicator
#align summable_subtype_iff_indicator summable_subtype_iff_indicator

@[simp]
theorem hasSum_subtype_support : HasSum (f âˆ˜ (â†‘) : support f â†’ Î±) a â†” HasSum f a :=
  hasSum_subtype_iff_of_support_subset <| Set.Subset.refl _
#align has_sum_subtype_support hasSum_subtype_support

theorem hasSum_fintype [Fintype Î²] (f : Î² â†’ Î±) : HasSum f (âˆ‘ b, f b) :=
  OrderTop.tendsto_atTop_nhds _
#align has_sum_fintype hasSum_fintype

protected theorem Finset.hasSum (s : Finset Î²) (f : Î² â†’ Î±) :
    HasSum (f âˆ˜ (â†‘) : (â†‘s : Set Î²) â†’ Î±) (âˆ‘ b in s, f b) := by
  rw [â† sum_attach]
  exact hasSum_fintype _
#align finset.has_sum Finset.hasSum

protected theorem Finset.summable (s : Finset Î²) (f : Î² â†’ Î±) :
    Summable (f âˆ˜ (â†‘) : (â†‘s : Set Î²) â†’ Î±) :=
  (s.hasSum f).summable
#align finset.summable Finset.summable

protected theorem Set.Finite.summable {s : Set Î²} (hs : s.Finite) (f : Î² â†’ Î±) :
    Summable (f âˆ˜ (â†‘) : s â†’ Î±) := by
  have := hs.toFinset.summable f
  rwa [hs.coe_toFinset] at this
#align set.finite.summable Set.Finite.summable

/-- If a function `f` vanishes outside of a finite set `s`, then it `HasSum` `âˆ‘ b in s, f b`. -/
theorem hasSum_sum_of_ne_finset_zero (hf : âˆ€ (b) (_ : b âˆ‰ s), f b = 0) : HasSum f (âˆ‘ b in s, f b) :=
  (hasSum_subtype_iff_of_support_subset <| support_subset_iff'.2 hf).1 <| s.hasSum f
#align has_sum_sum_of_ne_finset_zero hasSum_sum_of_ne_finset_zero

theorem summable_of_ne_finset_zero (hf : âˆ€ (b) (_ : b âˆ‰ s), f b = 0) : Summable f :=
  (hasSum_sum_of_ne_finset_zero hf).summable
#align summable_of_ne_finset_zero summable_of_ne_finset_zero

theorem summable_of_finite_support (h : (support f).Finite) : Summable f := by
  apply summable_of_ne_finset_zero (s := h.toFinset); simp

theorem Summable.hasSum (ha : Summable f) : HasSum f (âˆ‘' b, f b) := by
  simp only [tsum_def, ha, dite_true]
  by_cases H : (support f).Finite
  Â· simp [H, hasSum_sum_of_ne_finset_zero, finsum_eq_sum]
  Â· simpa [H] using Classical.choose_spec ha
#align summable.has_sum Summable.hasSum

theorem hasSum_single {f : Î² â†’ Î±} (b : Î²) (hf : âˆ€ (b') (_ : b' â‰  b), f b' = 0) : HasSum f (f b) :=
  suffices HasSum f (âˆ‘ b' in {b}, f b') by simpa using this
  hasSum_sum_of_ne_finset_zero <| by simpa [hf]
#align has_sum_single hasSum_single

@[simp] lemma hasSum_unique [Unique Î²] (f : Î² â†’ Î±) : HasSum f (f default) :=
  hasSum_single default (fun _ hb â†¦ False.elim <| hb <| Unique.uniq ..)

@[simp] lemma hasSum_singleton (m : Î²) (f : Î² â†’ Î±) : HasSum (({m} : Set Î²).restrict f) (f m) :=
  hasSum_unique (Set.restrict {m} f)

theorem hasSum_ite_eq (b : Î²) [DecidablePred (Â· = b)] (a : Î±) :
    HasSum (fun b' => if b' = b then a else 0) a := by
  convert @hasSum_single _ _ _ _ (fun b' => if b' = b then a else 0) b (fun b' hb' => if_neg hb')
  exact (if_pos rfl).symm
#align has_sum_ite_eq hasSum_ite_eq

theorem hasSum_pi_single [DecidableEq Î²] (b : Î²) (a : Î±) : HasSum (Pi.single b a) a := by
  convert hasSum_ite_eq b a
  simp [Pi.single_apply]
#align has_sum_pi_single hasSum_pi_single

theorem Equiv.hasSum_iff (e : Î³ â‰ƒ Î²) : HasSum (f âˆ˜ e) a â†” HasSum f a :=
  e.injective.hasSum_iff <| by simp
#align equiv.has_sum_iff Equiv.hasSum_iff

theorem Function.Injective.hasSum_range_iff {g : Î³ â†’ Î²} (hg : Injective g) :
    HasSum (fun x : Set.range g => f x) a â†” HasSum (f âˆ˜ g) a :=
  (Equiv.ofInjective g hg).hasSum_iff.symm
#align function.injective.has_sum_range_iff Function.Injective.hasSum_range_iff

theorem Equiv.summable_iff (e : Î³ â‰ƒ Î²) : Summable (f âˆ˜ e) â†” Summable f :=
  exists_congr fun _ => e.hasSum_iff
#align equiv.summable_iff Equiv.summable_iff

theorem Summable.prod_symm {f : Î² Ã— Î³ â†’ Î±} (hf : Summable f) : Summable fun p : Î³ Ã— Î² => f p.swap :=
  (Equiv.prodComm Î³ Î²).summable_iff.2 hf
#align summable.prod_symm Summable.prod_symm

theorem Equiv.hasSum_iff_of_support {g : Î³ â†’ Î±} (e : support f â‰ƒ support g)
    (he : âˆ€ x : support f, g (e x) = f x) : HasSum f a â†” HasSum g a := by
  have : (g âˆ˜ (â†‘)) âˆ˜ e = f âˆ˜ (â†‘) := funext he
  rw [â† hasSum_subtype_support, â† this, e.hasSum_iff, hasSum_subtype_support]
#align equiv.has_sum_iff_of_support Equiv.hasSum_iff_of_support

theorem hasSum_iff_hasSum_of_ne_zero_bij {g : Î³ â†’ Î±} (i : support g â†’ Î²)
    (hi : âˆ€ â¦ƒx yâ¦„, i x = i y â†’ (x : Î³) = y) (hf : support f âŠ† Set.range i)
    (hfg : âˆ€ x, f (i x) = g x) : HasSum f a â†” HasSum g a :=
  Iff.symm <|
    Equiv.hasSum_iff_of_support
      (Equiv.ofBijective (fun x => âŸ¨i x, fun hx => x.coe_prop <| hfg x â–¸ hxâŸ©)
        âŸ¨fun _ _ h => Subtype.ext <| hi <| Subtype.ext_iff.1 h, fun y =>
          (hf y.coe_prop).imp fun _ hx => Subtype.ext hxâŸ©)
      hfg
#align has_sum_iff_has_sum_of_ne_zero_bij hasSum_iff_hasSum_of_ne_zero_bij

theorem Equiv.summable_iff_of_support {g : Î³ â†’ Î±} (e : support f â‰ƒ support g)
    (he : âˆ€ x : support f, g (e x) = f x) : Summable f â†” Summable g :=
  exists_congr fun _ => e.hasSum_iff_of_support he
#align equiv.summable_iff_of_support Equiv.summable_iff_of_support

protected theorem HasSum.map [AddCommMonoid Î³] [TopologicalSpace Î³] (hf : HasSum f a) {G}
    [AddMonoidHomClass G Î± Î³] (g : G) (hg : Continuous g) : HasSum (g âˆ˜ f) (g a) :=
  have : (g âˆ˜ fun s : Finset Î² => âˆ‘ b in s, f b) = fun s : Finset Î² => âˆ‘ b in s, g (f b) :=
    funext <| map_sum g _
  show Tendsto (fun s : Finset Î² => âˆ‘ b in s, g (f b)) atTop (ğ“ (g a)) from
    this â–¸ (hg.tendsto a).comp hf
#align has_sum.map HasSum.map

protected theorem Summable.map [AddCommMonoid Î³] [TopologicalSpace Î³] (hf : Summable f) {G}
    [AddMonoidHomClass G Î± Î³] (g : G) (hg : Continuous g) : Summable (g âˆ˜ f) :=
  (hf.hasSum.map g hg).summable
#align summable.map Summable.map

protected theorem Summable.map_iff_of_leftInverse [AddCommMonoid Î³] [TopologicalSpace Î³] {G G'}
    [AddMonoidHomClass G Î± Î³] [AddMonoidHomClass G' Î³ Î±] (g : G) (g' : G') (hg : Continuous g)
    (hg' : Continuous g') (hinv : Function.LeftInverse g' g) : Summable (g âˆ˜ f) â†” Summable f :=
  âŸ¨fun h => by
    have := h.map _ hg'
    rwa [â† Function.comp.assoc, hinv.id] at this, fun h => h.map _ hgâŸ©
#align summable.map_iff_of_left_inverse Summable.map_iff_of_leftInverse

/-- A special case of `Summable.map_iff_of_leftInverse` for convenience -/
protected theorem Summable.map_iff_of_equiv [AddCommMonoid Î³] [TopologicalSpace Î³] {G}
    [AddEquivClass G Î± Î³] (g : G) (hg : Continuous g)
    (hg' : Continuous (AddEquivClass.toEquivLike.inv g : Î³ â†’ Î±)) : Summable (g âˆ˜ f) â†” Summable f :=
  Summable.map_iff_of_leftInverse g (g : Î± â‰ƒ+ Î³).symm hg hg' (AddEquivClass.toEquivLike.left_inv g)
#align summable.map_iff_of_equiv Summable.map_iff_of_equiv

/-- If `f : â„• â†’ Î±` has sum `a`, then the partial sums `âˆ‘_{i=0}^{n-1} f i` converge to `a`. -/
theorem HasSum.tendsto_sum_nat {f : â„• â†’ Î±} (h : HasSum f a) :
    Tendsto (fun n : â„• => âˆ‘ i in range n, f i) atTop (ğ“ a) :=
  h.comp tendsto_finset_range
#align has_sum.tendsto_sum_nat HasSum.tendsto_sum_nat

theorem HasSum.unique {aâ‚ aâ‚‚ : Î±} [T2Space Î±] : HasSum f aâ‚ â†’ HasSum f aâ‚‚ â†’ aâ‚ = aâ‚‚ := by
  classical exact tendsto_nhds_unique
#align has_sum.unique HasSum.unique

theorem Summable.hasSum_iff_tendsto_nat [T2Space Î±] {f : â„• â†’ Î±} {a : Î±} (hf : Summable f) :
    HasSum f a â†” Tendsto (fun n : â„• => âˆ‘ i in range n, f i) atTop (ğ“ a) := by
  refine' âŸ¨fun h => h.tendsto_sum_nat, fun h => _âŸ©
  rw [tendsto_nhds_unique h hf.hasSum.tendsto_sum_nat]
  exact hf.hasSum
#align summable.has_sum_iff_tendsto_nat Summable.hasSum_iff_tendsto_nat

theorem Function.Surjective.summable_iff_of_hasSum_iff {Î±' : Type*} [AddCommMonoid Î±']
    [TopologicalSpace Î±'] {e : Î±' â†’ Î±} (hes : Function.Surjective e) {f : Î² â†’ Î±} {g : Î³ â†’ Î±'}
    (he : âˆ€ {a}, HasSum f (e a) â†” HasSum g a) : Summable f â†” Summable g :=
  hes.exists.trans <| exists_congr <| @he
#align function.surjective.summable_iff_of_has_sum_iff Function.Surjective.summable_iff_of_hasSum_iff

variable [ContinuousAdd Î±]

theorem HasSum.add (hf : HasSum f a) (hg : HasSum g b) : HasSum (fun b => f b + g b) (a + b) := by
  dsimp only [HasSum] at hf hg âŠ¢
  simp_rw [sum_add_distrib]
  exact hf.add hg
#align has_sum.add HasSum.add

theorem Summable.add (hf : Summable f) (hg : Summable g) : Summable fun b => f b + g b :=
  (hf.hasSum.add hg.hasSum).summable
#align summable.add Summable.add

theorem hasSum_sum {f : Î³ â†’ Î² â†’ Î±} {a : Î³ â†’ Î±} {s : Finset Î³} :
    (âˆ€ i âˆˆ s, HasSum (f i) (a i)) â†’ HasSum (fun b => âˆ‘ i in s, f i b) (âˆ‘ i in s, a i) := by
  classical
  exact Finset.induction_on s (by simp only [hasSum_zero, sum_empty, forall_true_iff]) <| by
    -- Porting note: with some help, `simp` used to be able to close the goal
    simp (config := { contextual := true }) only [mem_insert, forall_eq_or_imp, not_false_iff,
      sum_insert, and_imp]
    exact fun x s _ IH hx h â†¦ hx.add (IH h)
#align has_sum_sum hasSum_sum

theorem summable_sum {f : Î³ â†’ Î² â†’ Î±} {s : Finset Î³} (hf : âˆ€ i âˆˆ s, Summable (f i)) :
    Summable fun b => âˆ‘ i in s, f i b :=
  (hasSum_sum fun i hi => (hf i hi).hasSum).summable
#align summable_sum summable_sum

theorem HasSum.add_disjoint {s t : Set Î²} (hs : Disjoint s t) (ha : HasSum (f âˆ˜ (â†‘) : s â†’ Î±) a)
    (hb : HasSum (f âˆ˜ (â†‘) : t â†’ Î±) b) : HasSum (f âˆ˜ (â†‘) : (s âˆª t : Set Î²) â†’ Î±) (a + b) := by
  rw [hasSum_subtype_iff_indicator] at *
  rw [Set.indicator_union_of_disjoint hs]
  exact ha.add hb
#align has_sum.add_disjoint HasSum.add_disjoint

theorem hasSum_sum_disjoint {Î¹} (s : Finset Î¹) {t : Î¹ â†’ Set Î²} {a : Î¹ â†’ Î±}
    (hs : (s : Set Î¹).Pairwise (Disjoint on t)) (hf : âˆ€ i âˆˆ s, HasSum (f âˆ˜ (â†‘) : t i â†’ Î±) (a i)) :
    HasSum (f âˆ˜ (â†‘) : (â‹ƒ i âˆˆ s, t i) â†’ Î±) (âˆ‘ i in s, a i) := by
  simp_rw [hasSum_subtype_iff_indicator] at *
  rw [Finset.indicator_biUnion _ _ hs]
  exact hasSum_sum hf
#align has_sum_sum_disjoint hasSum_sum_disjoint

theorem HasSum.add_isCompl {s t : Set Î²} (hs : IsCompl s t) (ha : HasSum (f âˆ˜ (â†‘) : s â†’ Î±) a)
    (hb : HasSum (f âˆ˜ (â†‘) : t â†’ Î±) b) : HasSum f (a + b) := by
  simpa [â† hs.compl_eq] using
    (hasSum_subtype_iff_indicator.1 ha).add (hasSum_subtype_iff_indicator.1 hb)
#align has_sum.add_is_compl HasSum.add_isCompl

theorem HasSum.add_compl {s : Set Î²} (ha : HasSum (f âˆ˜ (â†‘) : s â†’ Î±) a)
    (hb : HasSum (f âˆ˜ (â†‘) : (sá¶œ : Set Î²) â†’ Î±) b) : HasSum f (a + b) :=
  ha.add_isCompl isCompl_compl hb
#align has_sum.add_compl HasSum.add_compl

theorem Summable.add_compl {s : Set Î²} (hs : Summable (f âˆ˜ (â†‘) : s â†’ Î±))
    (hsc : Summable (f âˆ˜ (â†‘) : (sá¶œ : Set Î²) â†’ Î±)) : Summable f :=
  (hs.hasSum.add_compl hsc.hasSum).summable
#align summable.add_compl Summable.add_compl

theorem HasSum.compl_add {s : Set Î²} (ha : HasSum (f âˆ˜ (â†‘) : (sá¶œ : Set Î²) â†’ Î±) a)
    (hb : HasSum (f âˆ˜ (â†‘) : s â†’ Î±) b) : HasSum f (a + b) :=
  ha.add_isCompl isCompl_compl.symm hb
#align has_sum.compl_add HasSum.compl_add

theorem HasSum.even_add_odd {f : â„• â†’ Î±} (he : HasSum (fun k => f (2 * k)) a)
    (ho : HasSum (fun k => f (2 * k + 1)) b) : HasSum f (a + b) := by
  have := mul_right_injectiveâ‚€ (two_ne_zero' â„•)
  replace he := this.hasSum_range_iff.2 he
  replace ho := ((add_left_injective 1).comp this).hasSum_range_iff.2 ho
  refine' he.add_isCompl _ ho
  simpa [(Â· âˆ˜ Â·)] using Nat.isCompl_even_odd
#align has_sum.even_add_odd HasSum.even_add_odd

theorem Summable.compl_add {s : Set Î²} (hs : Summable (f âˆ˜ (â†‘) : (sá¶œ : Set Î²) â†’ Î±))
    (hsc : Summable (f âˆ˜ (â†‘) : s â†’ Î±)) : Summable f :=
  (hs.hasSum.compl_add hsc.hasSum).summable
#align summable.compl_add Summable.compl_add

theorem Summable.even_add_odd {f : â„• â†’ Î±} (he : Summable fun k => f (2 * k))
    (ho : Summable fun k => f (2 * k + 1)) : Summable f :=
  (he.hasSum.even_add_odd ho.hasSum).summable
#align summable.even_add_odd Summable.even_add_odd

theorem HasSum.sigma [RegularSpace Î±] {Î³ : Î² â†’ Type*} {f : (Î£ b : Î², Î³ b) â†’ Î±} {g : Î² â†’ Î±} {a : Î±}
    (ha : HasSum f a) (hf : âˆ€ b, HasSum (fun c => f âŸ¨b, câŸ©) (g b)) : HasSum g a := by
  classical
  refine' (atTop_basis.tendsto_iff (closed_nhds_basis a)).mpr _
  rintro s âŸ¨hs, hscâŸ©
  rcases mem_atTop_sets.mp (ha hs) with âŸ¨u, huâŸ©
  use u.image Sigma.fst, trivial
  intro bs hbs
  simp only [Set.mem_preimage, ge_iff_le, Finset.le_iff_subset] at hu
  have : Tendsto (fun t : Finset (Î£b, Î³ b) => âˆ‘ p in t.filter fun p => p.1 âˆˆ bs, f p) atTop
      (ğ“ <| âˆ‘ b in bs, g b) := by
    simp only [â† sigma_preimage_mk, sum_sigma]
    refine' tendsto_finset_sum _ fun b _ => _
    change
      Tendsto (fun t => (fun t => âˆ‘ s in t, f âŸ¨b, sâŸ©) (preimage t (Sigma.mk b) _)) atTop (ğ“ (g b))
    exact (hf b).comp (tendsto_finset_preimage_atTop_atTop (sigma_mk_injective))
  refine' hsc.mem_of_tendsto this (eventually_atTop.2 âŸ¨u, fun t ht => hu _ fun x hx => _âŸ©)
  exact mem_filter.2 âŸ¨ht hx, hbs <| mem_image_of_mem _ hxâŸ©
#align has_sum.sigma HasSum.sigma

/-- If a series `f` on `Î² Ã— Î³` has sum `a` and for each `b` the restriction of `f` to `{b} Ã— Î³`
has sum `g b`, then the series `g` has sum `a`. -/
theorem HasSum.prod_fiberwise [RegularSpace Î±] {f : Î² Ã— Î³ â†’ Î±} {g : Î² â†’ Î±} {a : Î±} (ha : HasSum f a)
    (hf : âˆ€ b, HasSum (fun c => f (b, c)) (g b)) : HasSum g a :=
  HasSum.sigma ((Equiv.sigmaEquivProd Î² Î³).hasSum_iff.2 ha) hf
#align has_sum.prod_fiberwise HasSum.prod_fiberwise

theorem Summable.sigma' [RegularSpace Î±] {Î³ : Î² â†’ Type*} {f : (Î£b : Î², Î³ b) â†’ Î±} (ha : Summable f)
    (hf : âˆ€ b, Summable fun c => f âŸ¨b, câŸ©) : Summable fun b => âˆ‘' c, f âŸ¨b, câŸ© :=
  (ha.hasSum.sigma fun b => (hf b).hasSum).summable
#align summable.sigma' Summable.sigma'

theorem HasSum.sigma_of_hasSum [T3Space Î±] {Î³ : Î² â†’ Type*} {f : (Î£b : Î², Î³ b) â†’ Î±} {g : Î² â†’ Î±}
    {a : Î±} (ha : HasSum g a) (hf : âˆ€ b, HasSum (fun c => f âŸ¨b, câŸ©) (g b)) (hf' : Summable f) :
    HasSum f a := by simpa [(hf'.hasSum.sigma hf).unique ha] using hf'.hasSum
#align has_sum.sigma_of_has_sum HasSum.sigma_of_hasSum

/-- Version of `HasSum.update` for `AddCommMonoid` rather than `AddCommGroup`.
Rather than showing that `f.update` has a specific sum in terms of `HasSum`,
it gives a relationship between the sums of `f` and `f.update` given that both exist. -/
theorem HasSum.update' {Î± Î² : Type*} [TopologicalSpace Î±] [AddCommMonoid Î±] [T2Space Î±]
    [ContinuousAdd Î±] [DecidableEq Î²] {f : Î² â†’ Î±} {a a' : Î±} (hf : HasSum f a) (b : Î²) (x : Î±)
    (hf' : HasSum (update f b x) a') : a + x = a' + f b := by
  have : âˆ€ b', f b' + ite (b' = b) x 0 = update f b x b' + ite (b' = b) (f b) 0 := by
    intro b'
    split_ifs with hb'
    Â· simpa only [Function.update_apply, hb', eq_self_iff_true] using add_comm (f b) x
    Â· simp only [Function.update_apply, hb', if_false]
  have h := hf.add (hasSum_ite_eq b x)
  simp_rw [this] at h
  exact HasSum.unique h (hf'.add (hasSum_ite_eq b (f b)))
#align has_sum.update' HasSum.update'

/-- Version of `hasSum_ite_sub_hasSum` for `AddCommMonoid` rather than `AddCommGroup`.
Rather than showing that the `ite` expression has a specific sum in terms of `HasSum`,
it gives a relationship between the sums of `f` and `ite (n = b) 0 (f n)` given that both exist. -/
theorem eq_add_of_hasSum_ite {Î± Î² : Type*} [TopologicalSpace Î±] [AddCommMonoid Î±] [T2Space Î±]
    [ContinuousAdd Î±] [DecidableEq Î²] {f : Î² â†’ Î±} {a : Î±} (hf : HasSum f a) (b : Î²) (a' : Î±)
    (hf' : HasSum (fun n => ite (n = b) 0 (f n)) a') : a = a' + f b := by
  refine' (add_zero a).symm.trans (hf.update' b 0 _)
  convert hf'
  apply update_apply
#align eq_add_of_has_sum_ite eq_add_of_hasSum_ite

end HasSum

section tsum

variable [AddCommMonoid Î±] [TopologicalSpace Î±] {f g : Î² â†’ Î±} {a aâ‚ aâ‚‚ : Î±}

theorem tsum_congr_set_coe (f : Î² â†’ Î±) {s t : Set Î²} (h : s = t) :
    âˆ‘' x : s, f x = âˆ‘' x : t, f x := by rw [h]
#align tsum_congr_subtype tsum_congr_set_coe

theorem tsum_congr_subtype (f : Î² â†’ Î±) {P Q : Î² â†’ Prop} (h : âˆ€ x, P x â†” Q x):
    âˆ‘' x : {x // P x}, f x = âˆ‘' x : {x // Q x}, f x :=
  tsum_congr_set_coe f <| Set.ext h

theorem tsum_eq_finsum (hf : (support f).Finite) :
    âˆ‘' b, f b = âˆ‘á¶  b, f b := by simp [tsum_def, summable_of_finite_support hf, hf]

theorem tsum_eq_sum {s : Finset Î²} (hf : âˆ€ (b) (_ : b âˆ‰ s), f b = 0) :
    âˆ‘' b, f b = âˆ‘ b in s, f b := by
  have I : support f âŠ† s := by
    intros x hx
    contrapose! hx
    rw [nmem_support]
    exact hf _ hx
  simp only [tsum_def, summable_of_ne_finset_zero hf, Set.Finite.subset (finite_toSet s) I,
     ite_true, dite_eq_ite]
  exact finsum_eq_sum_of_support_subset f I
#align tsum_eq_sum tsum_eq_sum

@[simp]
theorem tsum_zero : âˆ‘' _ : Î², (0 : Î±) = 0 := by rw [tsum_eq_finsum] <;> simp
#align tsum_zero tsum_zero
#align tsum_zero' tsum_zero

@[simp]
theorem tsum_empty [IsEmpty Î²] : âˆ‘' b, f b = 0 := by
  rw [tsum_eq_sum (s := (âˆ… : Finset Î²))] <;> simp
#align tsum_empty tsum_empty

theorem tsum_congr {f g : Î² â†’ Î±}
    (hfg : âˆ€ b, f b = g b) : âˆ‘' b, f b = âˆ‘' b, g b :=
  congr_arg tsum (funext hfg)
#align tsum_congr tsum_congr

theorem tsum_fintype [Fintype Î²] (f : Î² â†’ Î±) : âˆ‘' b, f b = âˆ‘ b, f b := by
  apply tsum_eq_sum; simp
#align tsum_fintype tsum_fintype

theorem sum_eq_tsum_indicator (f : Î² â†’ Î±) (s : Finset Î²) :
    âˆ‘ x in s, f x = âˆ‘' x, Set.indicator (â†‘s) f x :=
  have : âˆ€ (x) (_ : x âˆ‰ s), Set.indicator (â†‘s) f x = 0 := fun _ hx =>
    Set.indicator_apply_eq_zero.2 fun hx' => (hx <| Finset.mem_coe.1 hx').elim
  (Finset.sum_congr rfl fun _ hx =>
        (Set.indicator_apply_eq_self.2 fun hx' => (hx' <| Finset.mem_coe.2 hx).elim).symm).trans
    (tsum_eq_sum this).symm
#align sum_eq_tsum_indicator sum_eq_tsum_indicator

theorem tsum_bool (f : Bool â†’ Î±) : âˆ‘' i : Bool, f i = f false + f true := by
  rw [tsum_fintype, Fintype.sum_bool, add_comm]
#align tsum_bool tsum_bool

theorem tsum_eq_single {f : Î² â†’ Î±} (b : Î²) (hf : âˆ€ (b') (_ : b' â‰  b), f b' = 0) :
    âˆ‘' b, f b = f b := by
  rw [tsum_eq_sum (s := {b}), sum_singleton]
  exact fun b' hb' â†¦ hf b' (by simpa using hb')
  #align tsum_eq_single tsum_eq_single

theorem tsum_tsum_eq_single (f : Î² â†’ Î³ â†’ Î±) (b : Î²) (c : Î³) (hfb : âˆ€ (b') (_ : b' â‰  b), f b' c = 0)
    (hfc : âˆ€ (b' : Î²) (c' : Î³), c' â‰  c â†’ f b' c' = 0) : âˆ‘' (b') (c'), f b' c' = f b c :=
  calc
    âˆ‘' (b') (c'), f b' c' = âˆ‘' b', f b' c := tsum_congr fun b' => tsum_eq_single _ (hfc b')
    _ = f b c := tsum_eq_single _ hfb
#align tsum_tsum_eq_single tsum_tsum_eq_single

@[simp]
theorem tsum_ite_eq (b : Î²) [DecidablePred (Â· = b)] (a : Î±) :
    âˆ‘' b', (if b' = b then a else 0) = a := by
  rw [tsum_eq_single b]
  Â· simp
  Â· intro b' hb'; simp [hb']
#align tsum_ite_eq tsum_ite_eq

@[simp]
theorem tsum_pi_single [DecidableEq Î²] (b : Î²) (a : Î±) : âˆ‘' b', Pi.single b a b' = a := by
  rw [tsum_eq_single b]
  Â· simp
  Â· intro b' hb'; simp [hb']
#align tsum_pi_single tsum_pi_single

-- Porting note: Added nolint simpNF, simpNF falsely claims that lhs does not simplify under simp
@[simp, nolint simpNF]
theorem Finset.tsum_subtype (s : Finset Î²) (f : Î² â†’ Î±) :
    âˆ‘' x : { x // x âˆˆ s }, f x = âˆ‘ x in s, f x := by
  rw [â† sum_attach]; exact tsum_fintype _
#align finset.tsum_subtype Finset.tsum_subtype

theorem Finset.tsum_subtype' (s : Finset Î²) (f : Î² â†’ Î±) :
    âˆ‘' x : (s : Set Î²), f x = âˆ‘ x in s, f x := by simp
#align finset.tsum_subtype' Finset.tsum_subtype'

-- Porting note: Added nolint simpNF, simpNF falsely claims that lhs does not simplify under simp
@[simp, nolint simpNF]
theorem tsum_singleton (b : Î²) (f : Î² â†’ Î±) : âˆ‘' x : ({b} : Set Î²), f x = f b := by
  rw [â† coe_singleton, Finset.tsum_subtype', sum_singleton]
#align tsum_singleton tsum_singleton

variable [T2Space Î±]

theorem HasSum.tsum_eq (ha : HasSum f a) : âˆ‘' b, f b = a :=
  (Summable.hasSum âŸ¨a, haâŸ©).unique ha
#align has_sum.tsum_eq HasSum.tsum_eq

theorem Summable.hasSum_iff (h : Summable f) : HasSum f a â†” âˆ‘' b, f b = a :=
  Iff.intro HasSum.tsum_eq fun eq => eq â–¸ h.hasSum
#align summable.has_sum_iff Summable.hasSum_iff

theorem tsum_dite_right (P : Prop) [Decidable P] (x : Î² â†’ Â¬P â†’ Î±) :
    âˆ‘' b : Î², (if h : P then (0 : Î±) else x b h) = if h : P then (0 : Î±) else âˆ‘' b : Î², x b h := by
  by_cases hP : P <;> simp [hP]
#align tsum_dite_right tsum_dite_right

theorem tsum_dite_left (P : Prop) [Decidable P] (x : Î² â†’ P â†’ Î±) :
    âˆ‘' b : Î², (if h : P then x b h else 0) = if h : P then âˆ‘' b : Î², x b h else 0 := by
  by_cases hP : P <;> simp [hP]
#align tsum_dite_left tsum_dite_left

theorem Function.Surjective.tsum_eq_tsum_of_hasSum_iff_hasSum {Î±' : Type*} [AddCommMonoid Î±']
    [TopologicalSpace Î±'] {e : Î±' â†’ Î±} (hes : Function.Surjective e) (h0 : e 0 = 0) {f : Î² â†’ Î±}
    {g : Î³ â†’ Î±'} (h : âˆ€ {a}, HasSum f (e a) â†” HasSum g a) : âˆ‘' b, f b = e (âˆ‘' c, g c) :=
  _root_.by_cases (fun x => (h.mpr x.hasSum).tsum_eq) fun hg : Â¬Summable g => by
    have hf : Â¬Summable f := mt (hes.summable_iff_of_hasSum_iff @h).1 hg
    simp [tsum_def, hf, hg, h0]
#align function.surjective.tsum_eq_tsum_of_has_sum_iff_has_sum Function.Surjective.tsum_eq_tsum_of_hasSum_iff_hasSum

theorem tsum_eq_tsum_of_hasSum_iff_hasSum {f : Î² â†’ Î±} {g : Î³ â†’ Î±}
    (h : âˆ€ {a}, HasSum f a â†” HasSum g a) : âˆ‘' b, f b = âˆ‘' c, g c :=
  surjective_id.tsum_eq_tsum_of_hasSum_iff_hasSum rfl @h
#align tsum_eq_tsum_of_has_sum_iff_has_sum tsum_eq_tsum_of_hasSum_iff_hasSum

theorem Equiv.tsum_eq (j : Î³ â‰ƒ Î²) (f : Î² â†’ Î±) : âˆ‘' c, f (j c) = âˆ‘' b, f b :=
  tsum_eq_tsum_of_hasSum_iff_hasSum j.hasSum_iff
#align equiv.tsum_eq Equiv.tsum_eq

theorem Equiv.tsum_eq_tsum_of_support {f : Î² â†’ Î±} {g : Î³ â†’ Î±} (e : support f â‰ƒ support g)
    (he : âˆ€ x, g (e x) = f x) : âˆ‘' x, f x = âˆ‘' y, g y :=
  tsum_eq_tsum_of_hasSum_iff_hasSum (hasSum_iff_of_support e he)
#align equiv.tsum_eq_tsum_of_support Equiv.tsum_eq_tsum_of_support

theorem tsum_eq_tsum_of_ne_zero_bij {g : Î³ â†’ Î±} (i : support g â†’ Î²)
    (hi : âˆ€ â¦ƒx yâ¦„, i x = i y â†’ (x : Î³) = y) (hf : support f âŠ† Set.range i)
    (hfg : âˆ€ x, f (i x) = g x) : âˆ‘' x, f x = âˆ‘' y, g y :=
  tsum_eq_tsum_of_hasSum_iff_hasSum (hasSum_iff_hasSum_of_ne_zero_bij i hi hf hfg)
#align tsum_eq_tsum_of_ne_zero_bij tsum_eq_tsum_of_ne_zero_bij

@[simp]
lemma tsum_extend_zero {Î³ : Type*} {g : Î³ â†’ Î²} (hg : Injective g) (f : Î³ â†’ Î±) :
    âˆ‘' y, extend g f 0 y = âˆ‘' x, f x :=
  tsum_eq_tsum_of_hasSum_iff_hasSum <| hasSum_extend_zero hg

/-! ### `tsum` on subsets -/

theorem tsum_subtype (s : Set Î²) (f : Î² â†’ Î±) : âˆ‘' x : s, f x = âˆ‘' x, s.indicator f x :=
  tsum_eq_tsum_of_hasSum_iff_hasSum hasSum_subtype_iff_indicator
#align tsum_subtype tsum_subtype

theorem tsum_subtype_eq_of_support_subset {f : Î² â†’ Î±} {s : Set Î²} (hs : support f âŠ† s) :
    âˆ‘' x : s, f x = âˆ‘' x, f x :=
  tsum_eq_tsum_of_hasSum_iff_hasSum (hasSum_subtype_iff_of_support_subset hs)
#align tsum_subtype_eq_of_support_subset tsum_subtype_eq_of_support_subset

-- Porting note: Added nolint simpNF, simpNF falsely claims that lhs does not simplify under simp
@[simp, nolint simpNF]
theorem tsum_univ (f : Î² â†’ Î±) : âˆ‘' x : (Set.univ : Set Î²), f x = âˆ‘' x, f x :=
  tsum_subtype_eq_of_support_subset <| Set.subset_univ _
#align tsum_univ tsum_univ

theorem tsum_image {g : Î³ â†’ Î²} (f : Î² â†’ Î±) {s : Set Î³} (hg : Set.InjOn g s) :
    âˆ‘' x : g '' s, f x = âˆ‘' x : s, f (g x) :=
  ((Equiv.Set.imageOfInjOn _ _ hg).tsum_eq fun x => f x).symm
#align tsum_image tsum_image

theorem tsum_range {g : Î³ â†’ Î²} (f : Î² â†’ Î±) (hg : Injective g) :
    âˆ‘' x : Set.range g, f x = âˆ‘' x, f (g x) := by
  rw [â† Set.image_univ, tsum_image f (hg.injOn _)]
  simp_rw [â† comp_apply (g := g), tsum_univ (f âˆ˜ g)]
#align tsum_range tsum_range

/-- If `f b = 0` for all `b âˆˆ t`, then the sum over `f a` with `a âˆˆ s` is the same as the
sum over `f a` with `a âˆˆ s âˆ– t`. -/
lemma tsum_setElem_eq_tsum_setElem_diff [T2Space Î±] {f : Î² â†’ Î±} (s t : Set Î²)
    (hfâ‚€ : âˆ€ b âˆˆ t, f b = 0) :
    âˆ‘' a : s, f a = âˆ‘' a : (s \ t : Set Î²), f a :=
  tsum_eq_tsum_of_hasSum_iff_hasSum fun {a} â†¦ Iff.symm <|
    (Set.inclusion_injective <| s.diff_subset t).hasSum_iff
      (f := fun b : s â†¦ f b) fun b hb â†¦ hfâ‚€ b <| by simpa using hb

/-- If `f b = 0`, then the sum over `f a` with `a âˆˆ s` is the same as the sum over `f a` for
`a âˆˆ s âˆ– {b}`. -/
lemma tsum_eq_tsum_diff_singleton [T2Space Î±] {f : Î² â†’ Î±} (s : Set Î²) {b : Î²} (hfâ‚€ : f b = 0) :
    âˆ‘' a : s, f a = âˆ‘' a : (s \ {b} : Set Î²), f a :=
  tsum_setElem_eq_tsum_setElem_diff s {b} fun _ ha â†¦ ha â–¸ hfâ‚€

section ContinuousAdd

variable [ContinuousAdd Î±]

theorem tsum_add (hf : Summable f) (hg : Summable g) :
    âˆ‘' b, (f b + g b) = âˆ‘' b, f b + âˆ‘' b, g b :=
  (hf.hasSum.add hg.hasSum).tsum_eq
#align tsum_add tsum_add

theorem tsum_sum {f : Î³ â†’ Î² â†’ Î±} {s : Finset Î³} (hf : âˆ€ i âˆˆ s, Summable (f i)) :
    âˆ‘' b, âˆ‘ i in s, f i b = âˆ‘ i in s, âˆ‘' b, f i b :=
  (hasSum_sum fun i hi => (hf i hi).hasSum).tsum_eq
#align tsum_sum tsum_sum

/-- Version of `tsum_eq_add_tsum_ite` for `AddCommMonoid` rather than `AddCommGroup`.
Requires a different convergence assumption involving `Function.update`. -/
theorem tsum_eq_add_tsum_ite' [DecidableEq Î²] {f : Î² â†’ Î±} (b : Î²) (hf : Summable (update f b 0)) :
    âˆ‘' x, f x = f b + âˆ‘' x, ite (x = b) 0 (f x) :=
  calc
    âˆ‘' x, f x = âˆ‘' x, (ite (x = b) (f x) 0 + update f b 0 x) :=
      tsum_congr fun n => by split_ifs with h <;> simp [update_apply, h]
    _ = âˆ‘' x, ite (x = b) (f x) 0 + âˆ‘' x, update f b 0 x :=
      tsum_add âŸ¨ite (b = b) (f b) 0, hasSum_single b fun b hb => if_neg hbâŸ© hf
    _ = ite (b = b) (f b) 0 + âˆ‘' x, update f b 0 x := by
      congr
      exact tsum_eq_single b fun b' hb' => if_neg hb'
    _ = f b + âˆ‘' x, ite (x = b) 0 (f x) := by
      simp only [update, eq_self_iff_true, if_true, eq_rec_constant, dite_eq_ite]
#align tsum_eq_add_tsum_ite' tsum_eq_add_tsum_ite'

variable [AddCommMonoid Î´] [TopologicalSpace Î´] [T3Space Î´] [ContinuousAdd Î´]

theorem tsum_sigma' {Î³ : Î² â†’ Type*} {f : (Î£b : Î², Î³ b) â†’ Î´} (hâ‚ : âˆ€ b, Summable fun c => f âŸ¨b, câŸ©)
    (hâ‚‚ : Summable f) : âˆ‘' p, f p = âˆ‘' (b) (c), f âŸ¨b, câŸ© :=
  (hâ‚‚.hasSum.sigma fun b => (hâ‚ b).hasSum).tsum_eq.symm
#align tsum_sigma' tsum_sigma'

theorem tsum_prod' {f : Î² Ã— Î³ â†’ Î´} (h : Summable f) (hâ‚ : âˆ€ b, Summable fun c => f (b, c)) :
    âˆ‘' p, f p = âˆ‘' (b) (c), f (b, c) :=
  (h.hasSum.prod_fiberwise fun b => (hâ‚ b).hasSum).tsum_eq.symm
#align tsum_prod' tsum_prod'

theorem tsum_comm' {f : Î² â†’ Î³ â†’ Î´} (h : Summable (Function.uncurry f)) (hâ‚ : âˆ€ b, Summable (f b))
    (hâ‚‚ : âˆ€ c, Summable fun b => f b c) : âˆ‘' (c) (b), f b c = âˆ‘' (b) (c), f b c := by
  erw [â† tsum_prod' h hâ‚, â† tsum_prod' h.prod_symm hâ‚‚, â† (Equiv.prodComm Î³ Î²).tsum_eq (uncurry f)]
  rfl
#align tsum_comm' tsum_comm'

end ContinuousAdd

open Encodable

section Encodable

variable [Encodable Î³]

/-- You can compute a sum over an encodably type by summing over the natural numbers and
  taking a supremum. This is useful for outer measures. -/
theorem tsum_iSup_decodeâ‚‚ [CompleteLattice Î²] (m : Î² â†’ Î±) (m0 : m âŠ¥ = 0) (s : Î³ â†’ Î²) :
    âˆ‘' i : â„•, m (â¨† b âˆˆ decodeâ‚‚ Î³ i, s b) = âˆ‘' b : Î³, m (s b) := by
  have H : âˆ€ n, m (â¨† b âˆˆ decodeâ‚‚ Î³ n, s b) â‰  0 â†’ (decodeâ‚‚ Î³ n).isSome := by
    intro n h
    generalize decodeâ‚‚ Î³ n = foo at *
    cases' foo with b
    Â· refine' (h <| by simp [m0]).elim
    Â· exact rfl
  symm
  refine' tsum_eq_tsum_of_ne_zero_bij (fun a => Option.get _ (H a.1 a.2)) _ _ _
  Â· dsimp only []
    rintro âŸ¨m, hmâŸ© âŸ¨n, hnâŸ© e
    have := mem_decodeâ‚‚.1 (Option.get_mem (H n hn))
    rwa [â† e, mem_decodeâ‚‚.1 (Option.get_mem (H m hm))] at this
  Â· intro b h
    refine' âŸ¨âŸ¨encode b, _âŸ©, _âŸ©
    Â· simp only [mem_support, encodekâ‚‚] at h âŠ¢
      convert h
      simp [Set.ext_iff, encodekâ‚‚]
    Â· exact Option.get_of_mem _ (encodekâ‚‚ _)
  Â· rintro âŸ¨n, hâŸ©
    dsimp only [Subtype.coe_mk]
    trans
    swap
    rw [show decodeâ‚‚ Î³ n = _ from Option.get_mem (H n h)]
    congr
    simp [ext_iff, -Option.some_get]
#align tsum_supr_decodeâ‚‚ tsum_iSup_decodeâ‚‚

/-- `tsum_iSup_decodeâ‚‚` specialized to the complete lattice of sets. -/
theorem tsum_iUnion_decodeâ‚‚ (m : Set Î² â†’ Î±) (m0 : m âˆ… = 0) (s : Î³ â†’ Set Î²) :
    âˆ‘' i, m (â‹ƒ b âˆˆ decodeâ‚‚ Î³ i, s b) = âˆ‘' b, m (s b) :=
  tsum_iSup_decodeâ‚‚ m m0 s
#align tsum_Union_decodeâ‚‚ tsum_iUnion_decodeâ‚‚

end Encodable

/-! Some properties about measure-like functions.
  These could also be functions defined on complete sublattices of sets, with the property
  that they are countably sub-additive.
  `R` will probably be instantiated with `(â‰¤)` in all applications.
-/


section Countable

variable [Countable Î³]

/-- If a function is countably sub-additive then it is sub-additive on countable types -/
theorem rel_iSup_tsum [CompleteLattice Î²] (m : Î² â†’ Î±) (m0 : m âŠ¥ = 0) (R : Î± â†’ Î± â†’ Prop)
    (m_iSup : âˆ€ s : â„• â†’ Î², R (m (â¨† i, s i)) (âˆ‘' i, m (s i))) (s : Î³ â†’ Î²) :
    R (m (â¨† b : Î³, s b)) (âˆ‘' b : Î³, m (s b)) := by
  cases nonempty_encodable Î³
  rw [â† iSup_decodeâ‚‚, â† tsum_iSup_decodeâ‚‚ _ m0 s]
  exact m_iSup _
#align rel_supr_tsum rel_iSup_tsum

/-- If a function is countably sub-additive then it is sub-additive on finite sets -/
theorem rel_iSup_sum [CompleteLattice Î²] (m : Î² â†’ Î±) (m0 : m âŠ¥ = 0) (R : Î± â†’ Î± â†’ Prop)
    (m_iSup : âˆ€ s : â„• â†’ Î², R (m (â¨† i, s i)) (âˆ‘' i, m (s i))) (s : Î´ â†’ Î²) (t : Finset Î´) :
    R (m (â¨† d âˆˆ t, s d)) (âˆ‘ d in t, m (s d)) := by
  rw [iSup_subtype', â† Finset.tsum_subtype]
  exact rel_iSup_tsum m m0 R m_iSup _
#align rel_supr_sum rel_iSup_sum

/-- If a function is countably sub-additive then it is binary sub-additive -/
theorem rel_sup_add [CompleteLattice Î²] (m : Î² â†’ Î±) (m0 : m âŠ¥ = 0) (R : Î± â†’ Î± â†’ Prop)
    (m_iSup : âˆ€ s : â„• â†’ Î², R (m (â¨† i, s i)) (âˆ‘' i, m (s i))) (sâ‚ sâ‚‚ : Î²) :
    R (m (sâ‚ âŠ” sâ‚‚)) (m sâ‚ + m sâ‚‚) := by
  convert rel_iSup_tsum m m0 R m_iSup fun b => cond b sâ‚ sâ‚‚
  Â· simp only [iSup_bool_eq, cond]
  Â· rw [tsum_fintype, Fintype.sum_bool, cond, cond]
#align rel_sup_add rel_sup_add

end Countable

variable [ContinuousAdd Î±]

theorem tsum_add_tsum_compl {s : Set Î²} (hs : Summable (f âˆ˜ (â†‘) : s â†’ Î±))
    (hsc : Summable (f âˆ˜ (â†‘) : â†‘sá¶œ â†’ Î±)) : âˆ‘' x : s, f x + âˆ‘' x : â†‘sá¶œ, f x = âˆ‘' x, f x :=
  (hs.hasSum.add_compl hsc.hasSum).tsum_eq.symm
#align tsum_add_tsum_compl tsum_add_tsum_compl

theorem tsum_union_disjoint {s t : Set Î²} (hd : Disjoint s t) (hs : Summable (f âˆ˜ (â†‘) : s â†’ Î±))
    (ht : Summable (f âˆ˜ (â†‘) : t â†’ Î±)) : âˆ‘' x : â†‘(s âˆª t), f x = âˆ‘' x : s, f x + âˆ‘' x : t, f x :=
  (hs.hasSum.add_disjoint hd ht.hasSum).tsum_eq
#align tsum_union_disjoint tsum_union_disjoint

theorem tsum_finset_bUnion_disjoint {Î¹} {s : Finset Î¹} {t : Î¹ â†’ Set Î²}
    (hd : (s : Set Î¹).Pairwise (Disjoint on t)) (hf : âˆ€ i âˆˆ s, Summable (f âˆ˜ (â†‘) : t i â†’ Î±)) :
    âˆ‘' x : â‹ƒ i âˆˆ s, t i, f x = âˆ‘ i in s, âˆ‘' x : t i, f x :=
  (hasSum_sum_disjoint _ hd fun i hi => (hf i hi).hasSum).tsum_eq
#align tsum_finset_bUnion_disjoint tsum_finset_bUnion_disjoint

theorem tsum_even_add_odd {f : â„• â†’ Î±} (he : Summable fun k => f (2 * k))
    (ho : Summable fun k => f (2 * k + 1)) :
    âˆ‘' k, f (2 * k) + âˆ‘' k, f (2 * k + 1) = âˆ‘' k, f k :=
  (he.hasSum.even_add_odd ho.hasSum).tsum_eq.symm
#align tsum_even_add_odd tsum_even_add_odd

end tsum

section TopologicalGroup

variable [AddCommGroup Î±] [TopologicalSpace Î±] [TopologicalAddGroup Î±]

variable {f g : Î² â†’ Î±} {a aâ‚ aâ‚‚ : Î±}

-- `by simpa using` speeds up elaboration. Why?
theorem HasSum.neg (h : HasSum f a) : HasSum (fun b => -f b) (-a) := by
  simpa only using h.map (-AddMonoidHom.id Î±) continuous_neg
#align has_sum.neg HasSum.neg

theorem Summable.neg (hf : Summable f) : Summable fun b => -f b :=
  hf.hasSum.neg.summable
#align summable.neg Summable.neg

theorem Summable.of_neg (hf : Summable fun b => -f b) : Summable f := by
  simpa only [neg_neg] using hf.neg
#align summable.of_neg Summable.of_neg

theorem summable_neg_iff : (Summable fun b => -f b) â†” Summable f :=
  âŸ¨Summable.of_neg, Summable.negâŸ©
#align summable_neg_iff summable_neg_iff

theorem HasSum.sub (hf : HasSum f aâ‚) (hg : HasSum g aâ‚‚) :
    HasSum (fun b => f b - g b) (aâ‚ - aâ‚‚) := by
  simp only [sub_eq_add_neg]
  exact hf.add hg.neg
#align has_sum.sub HasSum.sub

theorem Summable.sub (hf : Summable f) (hg : Summable g) : Summable fun b => f b - g b :=
  (hf.hasSum.sub hg.hasSum).summable
#align summable.sub Summable.sub

theorem Summable.trans_sub (hg : Summable g) (hfg : Summable fun b => f b - g b) : Summable f := by
  simpa only [sub_add_cancel] using hfg.add hg
#align summable.trans_sub Summable.trans_sub

theorem summable_iff_of_summable_sub (hfg : Summable fun b => f b - g b) :
    Summable f â†” Summable g :=
  âŸ¨fun hf => hf.trans_sub <| by simpa only [neg_sub] using hfg.neg, fun hg => hg.trans_sub hfgâŸ©
#align summable_iff_of_summable_sub summable_iff_of_summable_sub

theorem HasSum.update (hf : HasSum f aâ‚) (b : Î²) [DecidableEq Î²] (a : Î±) :
    HasSum (update f b a) (a - f b + aâ‚) := by
  convert (hasSum_ite_eq b (a - f b)).add hf
  rename_i b'
  by_cases h : b' = b
  Â· rw [h, update_same]
    simp [eq_self_iff_true, if_true, sub_add_cancel]
  Â· simp only [h, update_noteq, if_false, Ne.def, zero_add, not_false_iff]
#align has_sum.update HasSum.update

theorem Summable.update (hf : Summable f) (b : Î²) [DecidableEq Î²] (a : Î±) :
    Summable (update f b a) :=
  (hf.hasSum.update b a).summable
#align summable.update Summable.update

theorem HasSum.hasSum_compl_iff {s : Set Î²} (hf : HasSum (f âˆ˜ (â†‘) : s â†’ Î±) aâ‚) :
    HasSum (f âˆ˜ (â†‘) : â†‘sá¶œ â†’ Î±) aâ‚‚ â†” HasSum f (aâ‚ + aâ‚‚) := by
  refine' âŸ¨fun h => hf.add_compl h, fun h => _âŸ©
  rw [hasSum_subtype_iff_indicator] at hf âŠ¢
  rw [Set.indicator_compl]
  simpa only [add_sub_cancel'] using h.sub hf
#align has_sum.has_sum_compl_iff HasSum.hasSum_compl_iff

theorem HasSum.hasSum_iff_compl {s : Set Î²} (hf : HasSum (f âˆ˜ (â†‘) : s â†’ Î±) aâ‚) :
    HasSum f aâ‚‚ â†” HasSum (f âˆ˜ (â†‘) : â†‘sá¶œ â†’ Î±) (aâ‚‚ - aâ‚) :=
  Iff.symm <| hf.hasSum_compl_iff.trans <| by rw [add_sub_cancel'_right]
#align has_sum.has_sum_iff_compl HasSum.hasSum_iff_compl

theorem Summable.summable_compl_iff {s : Set Î²} (hf : Summable (f âˆ˜ (â†‘) : s â†’ Î±)) :
    Summable (f âˆ˜ (â†‘) : â†‘sá¶œ â†’ Î±) â†” Summable f :=
  âŸ¨fun âŸ¨_, haâŸ© => (hf.hasSum.hasSum_compl_iff.1 ha).summable, fun âŸ¨_, haâŸ© =>
    (hf.hasSum.hasSum_iff_compl.1 ha).summableâŸ©
#align summable.summable_compl_iff Summable.summable_compl_iff

protected theorem Finset.hasSum_compl_iff (s : Finset Î²) :
    HasSum (fun x : { x // x âˆ‰ s } => f x) a â†” HasSum f (a + âˆ‘ i in s, f i) :=
  (s.hasSum f).hasSum_compl_iff.trans <| by rw [add_comm]
#align finset.has_sum_compl_iff Finset.hasSum_compl_iff

protected theorem Finset.hasSum_iff_compl (s : Finset Î²) :
    HasSum f a â†” HasSum (fun x : { x // x âˆ‰ s } => f x) (a - âˆ‘ i in s, f i) :=
  (s.hasSum f).hasSum_iff_compl
#align finset.has_sum_iff_compl Finset.hasSum_iff_compl

protected theorem Finset.summable_compl_iff (s : Finset Î²) :
    (Summable fun x : { x // x âˆ‰ s } => f x) â†” Summable f :=
  (s.summable f).summable_compl_iff
#align finset.summable_compl_iff Finset.summable_compl_iff

theorem Set.Finite.summable_compl_iff {s : Set Î²} (hs : s.Finite) :
    Summable (f âˆ˜ (â†‘) : â†‘sá¶œ â†’ Î±) â†” Summable f :=
  (hs.summable f).summable_compl_iff
#align set.finite.summable_compl_iff Set.Finite.summable_compl_iff

theorem hasSum_ite_sub_hasSum [DecidableEq Î²] (hf : HasSum f a) (b : Î²) :
    HasSum (fun n => ite (n = b) 0 (f n)) (a - f b) := by
  convert hf.update b 0 using 1
  Â· ext n
    rw [Function.update_apply]
  Â· rw [sub_add_eq_add_sub, zero_add]
#align has_sum_ite_sub_has_sum hasSum_ite_sub_hasSum

section tsum

variable [T2Space Î±]

theorem tsum_neg : âˆ‘' b, -f b = -âˆ‘' b, f b := by
  by_cases hf : Summable f
  Â· exact hf.hasSum.neg.tsum_eq
  Â· simp [tsum_eq_zero_of_not_summable hf, tsum_eq_zero_of_not_summable (mt Summable.of_neg hf)]
#align tsum_neg tsum_neg

theorem tsum_sub (hf : Summable f) (hg : Summable g) :
    âˆ‘' b, (f b - g b) = âˆ‘' b, f b - âˆ‘' b, g b :=
  (hf.hasSum.sub hg.hasSum).tsum_eq
#align tsum_sub tsum_sub

theorem sum_add_tsum_compl {s : Finset Î²} (hf : Summable f) :
    ((âˆ‘ x in s, f x) + âˆ‘' x : â†‘(s : Set Î²)á¶œ, f x) = âˆ‘' x, f x :=
  ((s.hasSum f).add_compl (s.summable_compl_iff.2 hf).hasSum).tsum_eq.symm
#align sum_add_tsum_compl sum_add_tsum_compl

/-- Let `f : Î² â†’ Î±` be a sequence with summable series and let `b âˆˆ Î²` be an index.
Lemma `tsum_eq_add_tsum_ite` writes `Î£ f n` as the sum of `f b` plus the series of the
remaining terms. -/
theorem tsum_eq_add_tsum_ite [DecidableEq Î²] (hf : Summable f) (b : Î²) :
    âˆ‘' n, f n = f b + âˆ‘' n, ite (n = b) 0 (f n) := by
  rw [(hasSum_ite_sub_hasSum hf.hasSum b).tsum_eq]
  exact (add_sub_cancel'_right _ _).symm
#align tsum_eq_add_tsum_ite tsum_eq_add_tsum_ite

end tsum

/-!
### Sums on nat

We show the formula `âˆ‘ i in range k, f i + âˆ‘' i, f (i + k) = âˆ‘' i, f i`, in
`sum_add_tsum_nat_add`, as well as several results relating sums on `â„•` and `â„¤`.
-/

section Nat

theorem hasSum_nat_add_iff {f : â„• â†’ Î±} (k : â„•) {a : Î±} :
    HasSum (fun n => f (n + k)) a â†” HasSum f (a + âˆ‘ i in range k, f i) := by
  refine' Iff.trans _ (range k).hasSum_compl_iff
  rw [â† (notMemRangeEquiv k).symm.hasSum_iff]
  rfl
#align has_sum_nat_add_iff hasSum_nat_add_iff

theorem summable_nat_add_iff {f : â„• â†’ Î±} (k : â„•) : (Summable fun n => f (n + k)) â†” Summable f :=
  Iff.symm <|
    (Equiv.addRight (âˆ‘ i in range k, f i)).surjective.summable_iff_of_hasSum_iff
      (hasSum_nat_add_iff k).symm
#align summable_nat_add_iff summable_nat_add_iff

theorem hasSum_nat_add_iff' {f : â„• â†’ Î±} (k : â„•) {a : Î±} :
    HasSum (fun n => f (n + k)) (a - âˆ‘ i in range k, f i) â†” HasSum f a := by
  simp [hasSum_nat_add_iff]
#align has_sum_nat_add_iff' hasSum_nat_add_iff'

theorem HasSum.sum_range_add [AddCommMonoid M] [TopologicalSpace M] [ContinuousAdd M] {f : â„• â†’ M}
    {k : â„•} {a : M} (h : HasSum (fun n â†¦ f (n + k)) a) : HasSum f ((âˆ‘ i in range k, f i) + a) := by
  refine ((range k).hasSum f).add_compl ?_
  rwa [â† (notMemRangeEquiv k).symm.hasSum_iff]

theorem sum_add_tsum_nat_add' [AddCommMonoid M] [TopologicalSpace M] [ContinuousAdd M] [T2Space M]
    {f : â„• â†’ M} {k : â„•} (h : Summable (fun n => f (n + k))) :
    ((âˆ‘ i in range k, f i) + âˆ‘' i, f (i + k)) = âˆ‘' i, f i :=
  h.hasSum.sum_range_add.tsum_eq.symm

theorem sum_add_tsum_nat_add [T2Space Î±] {f : â„• â†’ Î±} (k : â„•) (h : Summable f) :
    ((âˆ‘ i in range k, f i) + âˆ‘' i, f (i + k)) = âˆ‘' i, f i :=
  sum_add_tsum_nat_add' <| (summable_nat_add_iff k).2 h
#align sum_add_tsum_nat_add sum_add_tsum_nat_add

theorem tsum_eq_zero_add' [AddCommMonoid M] [TopologicalSpace M] [ContinuousAdd M] [T2Space M]
    {f : â„• â†’ M} (hf : Summable (fun n => f (n + 1))) :
    âˆ‘' b, f b = f 0 + âˆ‘' b, f (b + 1) := by
  simpa only [sum_range_one] using (sum_add_tsum_nat_add' hf).symm

theorem tsum_eq_zero_add [T2Space Î±] {f : â„• â†’ Î±} (hf : Summable f) :
    âˆ‘' b, f b = f 0 + âˆ‘' b, f (b + 1) :=
  tsum_eq_zero_add' <| (summable_nat_add_iff 1).2 hf
#align tsum_eq_zero_add tsum_eq_zero_add

/-- For `f : â„• â†’ Î±`, then `âˆ‘' k, f (k + i)` tends to zero. This does not require a summability
assumption on `f`, as otherwise all sums are zero. -/
theorem tendsto_sum_nat_add [T2Space Î±] (f : â„• â†’ Î±) :
    Tendsto (fun i => âˆ‘' k, f (k + i)) atTop (ğ“ 0) := by
  by_cases hf : Summable f
  Â· have hâ‚€ : (fun i => âˆ‘' i, f i - âˆ‘ j in range i, f j) = fun i => âˆ‘' k : â„•, f (k + i) := by
      ext1 i
      rw [sub_eq_iff_eq_add, add_comm, sum_add_tsum_nat_add i hf]
    have hâ‚ : Tendsto (fun _ : â„• => âˆ‘' i, f i) atTop (ğ“ (âˆ‘' i, f i)) := tendsto_const_nhds
    simpa only [hâ‚€, sub_self] using Tendsto.sub hâ‚ hf.hasSum.tendsto_sum_nat
  Â· refine tendsto_const_nhds.congr fun n â†¦ (tsum_eq_zero_of_not_summable ?_).symm
    rwa [summable_nat_add_iff n]
#align tendsto_sum_nat_add tendsto_sum_nat_add

/-- If `fâ‚€, fâ‚, fâ‚‚, ...` and `gâ‚€, gâ‚, gâ‚‚, ...` are both convergent then so is the `â„¤`-indexed
sequence: `..., gâ‚‚, gâ‚, gâ‚€, fâ‚€, fâ‚, fâ‚‚, ...`. -/
theorem HasSum.int_rec {b : Î±} {f g : â„• â†’ Î±} (hf : HasSum f a) (hg : HasSum g b) :
    @HasSum Î± _ _ _ (@Int.rec (fun _ => Î±) f g : â„¤ â†’ Î±) (a + b) := by
  -- note this proof works for any two-case inductive
  have hâ‚ : Injective ((â†‘) : â„• â†’ â„¤) := @Int.ofNat.inj
  have hâ‚‚ : Injective Int.negSucc := @Int.negSucc.inj
  have : IsCompl (Set.range ((â†‘) : â„• â†’ â„¤)) (Set.range Int.negSucc) := by
    constructor
    Â· rw [disjoint_iff_inf_le]
      rintro _ âŸ¨âŸ¨i, rflâŸ©, âŸ¨j, âŸ¨âŸ©âŸ©âŸ©
    Â· rw [codisjoint_iff_le_sup]
      rintro (i | j) _
      exacts [Or.inl âŸ¨_, rflâŸ©, Or.inr âŸ¨_, rflâŸ©]
  exact HasSum.add_isCompl this (hâ‚.hasSum_range_iff.mpr hf) (hâ‚‚.hasSum_range_iff.mpr hg)
#align has_sum.int_rec HasSum.int_rec

theorem HasSum.nonneg_add_neg {b : Î±} {f : â„¤ â†’ Î±} (hnonneg : HasSum (fun n : â„• => f n) a)
    (hneg : HasSum (fun n : â„• => f (-n.succ)) b) : HasSum f (a + b) := by
  simp_rw [â† Int.negSucc_coe] at hneg
  convert hnonneg.int_rec hneg using 1
  ext (i | j) <;> rfl
#align has_sum.nonneg_add_neg HasSum.nonneg_add_neg

theorem HasSum.pos_add_zero_add_neg {b : Î±} {f : â„¤ â†’ Î±} (hpos : HasSum (fun n : â„• => f (n + 1)) a)
    (hneg : HasSum (fun n : â„• => f (-n.succ)) b) : HasSum f (a + f 0 + b) :=
  haveI : âˆ€ g : â„• â†’ Î±, HasSum (fun k => g (k + 1)) a â†’ HasSum g (a + g 0) := by
    intro g hg
    simpa using (hasSum_nat_add_iff _).mp hg
  (this (fun n => f n) hpos).nonneg_add_neg hneg
#align has_sum.pos_add_zero_add_neg HasSum.pos_add_zero_add_neg

theorem summable_int_of_summable_nat {f : â„¤ â†’ Î±} (hp : Summable fun n : â„• => f n)
    (hn : Summable fun n : â„• => f (-n)) : Summable f :=
  (HasSum.nonneg_add_neg hp.hasSum <| Summable.hasSum <| (summable_nat_add_iff 1).mpr hn).summable
#align summable_int_of_summable_nat summable_int_of_summable_nat

theorem HasSum.sum_nat_of_sum_int {Î± : Type*} [AddCommMonoid Î±] [TopologicalSpace Î±]
    [ContinuousAdd Î±] {a : Î±} {f : â„¤ â†’ Î±} (hf : HasSum f a) :
    HasSum (fun n : â„• => f n + f (-n)) (a + f 0) := by
  apply (hf.add (hasSum_ite_eq (0 : â„¤) (f 0))).hasSum_of_sum_eq fun u => ?_
  refine' âŸ¨u.image Int.natAbs, fun v' hv' => _âŸ©
  let u1 := v'.image fun x : â„• => (x : â„¤)
  let u2 := v'.image fun x : â„• => -(x : â„¤)
  have A : u âŠ† u1 âˆª u2 := by
    intro x hx
    simp only [mem_union, mem_image, exists_prop]
    rcases le_total 0 x with (h'x | h'x)
    Â· left
      refine' âŸ¨Int.natAbs x, hv' _, _âŸ©
      Â· simp only [mem_image, exists_prop]
        exact âŸ¨x, hx, rflâŸ©
      Â· simp only [h'x, Int.coe_natAbs, abs_eq_self]
    Â· right
      refine' âŸ¨Int.natAbs x, hv' _, _âŸ©
      Â· simp only [mem_image, exists_prop]
        exact âŸ¨x, hx, rflâŸ©
      Â· simp only [abs_of_nonpos h'x, Int.coe_natAbs, neg_neg]
  refine' âŸ¨u1 âˆª u2, A, _âŸ©
  calc
    (âˆ‘ x in u1 âˆª u2, (f x + ite (x = 0) (f 0) 0)) =
        (âˆ‘ x in u1 âˆª u2, f x) + âˆ‘ x in u1 âˆ© u2, f x := by
      rw [sum_add_distrib]
      congr 1
      refine' (sum_subset_zero_on_sdiff inter_subset_union _ _).symm
      Â· intro x hx
        suffices x â‰  0 by simp only [this, if_false]
        rintro rfl
        simp at hx
      Â· intro x hx
        simp only [mem_inter, mem_image, exists_prop] at hx
        have : x = 0 := by
          apply le_antisymm
          Â· rcases hx.2 with âŸ¨a, _, rflâŸ©
            simp only [Right.neg_nonpos_iff, Nat.cast_nonneg]
          Â· rcases hx.1 with âŸ¨a, _, rflâŸ©
            simp only [Nat.cast_nonneg]
        simp only [this, eq_self_iff_true, if_true]
    _ = (âˆ‘ x in u1, f x) + âˆ‘ x in u2, f x := sum_union_inter
    _ = (âˆ‘ b in v', f b) + âˆ‘ b in v', f (-b) := by simp
    _ = âˆ‘ b in v', (f b + f (-b)) := sum_add_distrib.symm
#align has_sum.sum_nat_of_sum_int HasSum.sum_nat_of_sum_int

end Nat

end TopologicalGroup

section UniformGroup

variable [AddCommGroup Î±] [UniformSpace Î±]

/-- The **Cauchy criterion** for infinite sums, also known as the **Cauchy convergence test** -/
theorem summable_iff_cauchySeq_finset [CompleteSpace Î±] {f : Î² â†’ Î±} :
    Summable f â†” CauchySeq fun s : Finset Î² â†¦ âˆ‘ b in s, f b := by
  classical exact cauchy_map_iff_exists_tendsto.symm
#align summable_iff_cauchy_seq_finset summable_iff_cauchySeq_finset

variable [UniformAddGroup Î±] {f g : Î² â†’ Î±} {a aâ‚ aâ‚‚ : Î±}

theorem cauchySeq_finset_iff_vanishing :
    (CauchySeq fun s : Finset Î² â†¦ âˆ‘ b in s, f b) â†”
      âˆ€ e âˆˆ ğ“ (0 : Î±), âˆƒ s : Finset Î², âˆ€ t, Disjoint t s â†’ (âˆ‘ b in t, f b) âˆˆ e := by
  classical
  simp only [CauchySeq, cauchy_map_iff, and_iff_right atTop_neBot, prod_atTop_atTop_eq,
    uniformity_eq_comap_nhds_zero Î±, tendsto_comap_iff, (Â· âˆ˜ Â·), atTop_neBot, true_and]
  rw [tendsto_atTop']
  constructor
  Â· intro h e he
    obtain âŸ¨âŸ¨sâ‚, sâ‚‚âŸ©, hâŸ© := h e he
    use sâ‚ âˆª sâ‚‚
    intro t ht
    specialize h (sâ‚ âˆª sâ‚‚, sâ‚ âˆª sâ‚‚ âˆª t) âŸ¨le_sup_left, le_sup_of_le_left le_sup_rightâŸ©
    simpa only [Finset.sum_union ht.symm, add_sub_cancel'] using h
  Â· rintro h e he
    rcases exists_nhds_half_neg he with âŸ¨d, hd, hdeâŸ©
    rcases h d hd with âŸ¨s, hâŸ©
    use (s, s)
    rintro âŸ¨tâ‚, tâ‚‚âŸ© âŸ¨htâ‚, htâ‚‚âŸ©
    have : ((âˆ‘ b in tâ‚‚, f b) - âˆ‘ b in tâ‚, f b) = (âˆ‘ b in tâ‚‚ \ s, f b) - âˆ‘ b in tâ‚ \ s, f b := by
      rw [â† Finset.sum_sdiff htâ‚, â† Finset.sum_sdiff htâ‚‚, add_sub_add_right_eq_sub]
    simp only [this]
    exact hde _ (h _ Finset.sdiff_disjoint) _ (h _ Finset.sdiff_disjoint)
#align cauchy_seq_finset_iff_vanishing cauchySeq_finset_iff_vanishing

theorem cauchySeq_finset_iff_tsum_vanishing :
    (CauchySeq fun s : Finset Î² â†¦ âˆ‘ b in s, f b) â†”
      âˆ€ e âˆˆ ğ“ (0 : Î±), âˆƒ s : Finset Î², âˆ€ t : Set Î², Disjoint t s â†’ (âˆ‘' b : t, f b) âˆˆ e := by
  simp_rw [cauchySeq_finset_iff_vanishing, Set.disjoint_left, disjoint_left]
  refine âŸ¨fun vanish e he â†¦ ?_, fun vanish e he â†¦ ?_âŸ©
  Â· obtain âŸ¨o, ho, o_closed, oeâŸ© := exists_mem_nhds_isClosed_subset he
    obtain âŸ¨s, hsâŸ© := vanish o ho
    refine âŸ¨s, fun t hts â†¦ oe ?_âŸ©
    by_cases ht : Summable fun a : t â†¦ f a
    Â· classical
      refine o_closed.mem_of_tendsto ht.hasSum (eventually_of_forall fun t' â†¦ ?_)
      rw [â† sum_subtype_map_embedding fun _ _ â†¦ by rfl]
      apply hs
      simp_rw [Finset.mem_map]
      rintro _ âŸ¨b, -, rflâŸ©
      exact hts b.prop
    Â· exact tsum_eq_zero_of_not_summable ht â–¸ mem_of_mem_nhds ho
  Â· obtain âŸ¨s, hsâŸ© := vanish _ he
    exact âŸ¨s, fun t hts â†¦ (t.tsum_subtype f).symm â–¸ hs _ htsâŸ©

theorem cauchySeq_finset_iff_nat_tsum_vanishing {f : â„• â†’ Î±} :
    (CauchySeq fun s : Finset â„• â†¦ âˆ‘ n in s, f n) â†”
      âˆ€ e âˆˆ ğ“ (0 : Î±), âˆƒ N : â„•, âˆ€ t âŠ† {n | N â‰¤ n}, (âˆ‘' n : t, f n) âˆˆ e := by
  refine cauchySeq_finset_iff_tsum_vanishing.trans âŸ¨fun vanish e he â†¦ ?_, fun vanish e he â†¦ ?_âŸ©
  Â· obtain âŸ¨s, hsâŸ© := vanish e he
    refine âŸ¨if h : s.Nonempty then s.max' h + 1 else 0, fun t ht â†¦ hs _ <| Set.disjoint_left.mpr ?_âŸ©
    split_ifs at ht with h
    Â· exact fun m hmt hms â†¦ (s.le_max' _ hms).not_lt (Nat.succ_le_iff.mp <| ht hmt)
    Â· exact fun _ _ hs â†¦ h âŸ¨_, hsâŸ©
  Â· obtain âŸ¨N, hNâŸ© := vanish e he
    exact âŸ¨range N, fun t ht â†¦ hN _ fun n hnt â†¦
      le_of_not_lt fun h â†¦ Set.disjoint_left.mp ht hnt (mem_range.mpr h)âŸ©

variable [CompleteSpace Î±]

theorem summable_iff_vanishing :
    Summable f â†” âˆ€ e âˆˆ ğ“ (0 : Î±), âˆƒ s : Finset Î², âˆ€ t, Disjoint t s â†’ (âˆ‘ b in t, f b) âˆˆ e := by
  rw [summable_iff_cauchySeq_finset, cauchySeq_finset_iff_vanishing]
#align summable_iff_vanishing summable_iff_vanishing

theorem summable_iff_tsum_vanishing : Summable f â†”
    âˆ€ e âˆˆ ğ“ (0 : Î±), âˆƒ s : Finset Î², âˆ€ t : Set Î², Disjoint t s â†’ (âˆ‘' b : t, f b) âˆˆ e := by
  rw [summable_iff_cauchySeq_finset, cauchySeq_finset_iff_tsum_vanishing]

theorem summable_iff_nat_tsum_vanishing {f : â„• â†’ Î±} : Summable f â†”
    âˆ€ e âˆˆ ğ“ (0 : Î±), âˆƒ N : â„•, âˆ€ t âŠ† {n | N â‰¤ n}, (âˆ‘' n : t, f n) âˆˆ e := by
  rw [summable_iff_cauchySeq_finset, cauchySeq_finset_iff_nat_tsum_vanishing]

-- TODO: generalize to monoid with a uniform continuous subtraction operator: `(a + b) - b = a`
theorem Summable.summable_of_eq_zero_or_self (hf : Summable f) (h : âˆ€ b, g b = 0 âˆ¨ g b = f b) :
    Summable g := by
  classical
  exact summable_iff_vanishing.2 fun e he =>
    let âŸ¨s, hsâŸ© := summable_iff_vanishing.1 hf e he
    âŸ¨s, fun t ht =>
      have eq : âˆ‘ b in t.filter fun b => g b = f b, f b = âˆ‘ b in t, g b :=
        calc
          âˆ‘ b in t.filter fun b => g b = f b, f b = âˆ‘ b in t.filter fun b => g b = f b, g b :=
            Finset.sum_congr rfl fun b hb => (Finset.mem_filter.1 hb).2.symm
          _ = âˆ‘ b in t, g b := by
           {refine' Finset.sum_subset (Finset.filter_subset _ _) _
            intro b hbt hb
            simp only [Finset.mem_filter, and_iff_right hbt] at hb
            exact (h b).resolve_right hb}
      eq â–¸ hs _ <| Finset.disjoint_of_subset_left (Finset.filter_subset _ _) htâŸ©
#align summable.summable_of_eq_zero_or_self Summable.summable_of_eq_zero_or_self

protected theorem Summable.indicator (hf : Summable f) (s : Set Î²) : Summable (s.indicator f) :=
  hf.summable_of_eq_zero_or_self <| Set.indicator_eq_zero_or_self _ _
#align summable.indicator Summable.indicator

theorem Summable.comp_injective {i : Î³ â†’ Î²} (hf : Summable f) (hi : Injective i) :
    Summable (f âˆ˜ i) := by
  simpa only [Set.indicator_range_comp] using
    (hi.summable_iff (fun x hx => Set.indicator_of_not_mem hx _)).2 (hf.indicator (Set.range i))
#align summable.comp_injective Summable.comp_injective

theorem Summable.subtype (hf : Summable f) (s : Set Î²) : Summable (f âˆ˜ (â†‘) : s â†’ Î±) :=
  hf.comp_injective Subtype.coe_injective
#align summable.subtype Summable.subtype

theorem summable_subtype_and_compl {s : Set Î²} :
    ((Summable fun x : s => f x) âˆ§ Summable fun x : â†‘sá¶œ => f x) â†” Summable f :=
  âŸ¨and_imp.2 Summable.add_compl, fun h => âŸ¨h.subtype s, h.subtype sá¶œâŸ©âŸ©
#align summable_subtype_and_compl summable_subtype_and_compl

theorem Summable.sigma_factor {Î³ : Î² â†’ Type*} {f : (Î£b : Î², Î³ b) â†’ Î±} (ha : Summable f) (b : Î²) :
    Summable fun c => f âŸ¨b, câŸ© :=
  ha.comp_injective sigma_mk_injective
#align summable.sigma_factor Summable.sigma_factor

theorem Summable.sigma {Î³ : Î² â†’ Type*} {f : (Î£b : Î², Î³ b) â†’ Î±} (ha : Summable f) :
    Summable fun b => âˆ‘' c, f âŸ¨b, câŸ© :=
  ha.sigma' fun b => ha.sigma_factor b
#align summable.sigma Summable.sigma

theorem Summable.prod_factor {f : Î² Ã— Î³ â†’ Î±} (h : Summable f) (b : Î²) :
    Summable fun c => f (b, c) :=
  h.comp_injective fun _ _ h => (Prod.ext_iff.1 h).2
#align summable.prod_factor Summable.prod_factor

theorem tsum_sigma [T0Space Î±] {Î³ : Î² â†’ Type*} {f : (Î£b : Î², Î³ b) â†’ Î±} (ha : Summable f) :
    âˆ‘' p, f p = âˆ‘' (b) (c), f âŸ¨b, câŸ© :=
  tsum_sigma' (fun b => ha.sigma_factor b) ha
#align tsum_sigma tsum_sigma

theorem tsum_prod [T0Space Î±] {f : Î² Ã— Î³ â†’ Î±} (h : Summable f) :
    âˆ‘' p, f p = âˆ‘' (b) (c), f âŸ¨b, câŸ© :=
  tsum_prod' h h.prod_factor
#align tsum_prod tsum_prod

theorem tsum_comm [T0Space Î±] {f : Î² â†’ Î³ â†’ Î±} (h : Summable (Function.uncurry f)) :
    âˆ‘' (c) (b), f b c = âˆ‘' (b) (c), f b c :=
  tsum_comm' h h.prod_factor h.prod_symm.prod_factor
#align tsum_comm tsum_comm

theorem tsum_subtype_add_tsum_subtype_compl [T2Space Î±] {f : Î² â†’ Î±} (hf : Summable f) (s : Set Î²) :
    âˆ‘' x : s, f x + âˆ‘' x : â†‘sá¶œ, f x = âˆ‘' x, f x :=
  ((hf.subtype s).hasSum.add_compl (hf.subtype { x | x âˆ‰ s }).hasSum).unique hf.hasSum
#align tsum_subtype_add_tsum_subtype_compl tsum_subtype_add_tsum_subtype_compl

theorem sum_add_tsum_subtype_compl [T2Space Î±] {f : Î² â†’ Î±} (hf : Summable f) (s : Finset Î²) :
    âˆ‘ x in s, f x + âˆ‘' x : { x // x âˆ‰ s }, f x = âˆ‘' x, f x := by
  rw [â† tsum_subtype_add_tsum_subtype_compl hf s]
  simp only [Finset.tsum_subtype', add_right_inj]
  rfl
#align sum_add_tsum_subtype_compl sum_add_tsum_subtype_compl

end UniformGroup

section TopologicalGroup

variable {G : Type*} [TopologicalSpace G] [AddCommGroup G] [TopologicalAddGroup G] {f : Î± â†’ G}

theorem Summable.vanishing (hf : Summable f) â¦ƒe : Set Gâ¦„ (he : e âˆˆ ğ“ (0 : G)) :
    âˆƒ s : Finset Î±, âˆ€ t, Disjoint t s â†’ (âˆ‘ k in t, f k) âˆˆ e := by
  classical
  letI : UniformSpace G := TopologicalAddGroup.toUniformSpace G
  have : UniformAddGroup G := comm_topologicalAddGroup_is_uniform
  exact cauchySeq_finset_iff_vanishing.1 hf.hasSum.cauchySeq e he
#align summable.vanishing Summable.vanishing

theorem Summable.tsum_vanishing (hf : Summable f) â¦ƒe : Set Gâ¦„ (he : e âˆˆ ğ“ 0) :
    âˆƒ s : Finset Î±, âˆ€ t : Set Î±, Disjoint t s â†’ (âˆ‘' b : t, f b) âˆˆ e := by
  classical
  letI : UniformSpace G := TopologicalAddGroup.toUniformSpace G
  have : UniformAddGroup G := comm_topologicalAddGroup_is_uniform
  exact cauchySeq_finset_iff_tsum_vanishing.1 hf.hasSum.cauchySeq e he

/-- The sum over the complement of a finset tends to `0` when the finset grows to cover the whole
space. This does not need a summability assumption, as otherwise all sums are zero. -/
theorem tendsto_tsum_compl_atTop_zero (f : Î± â†’ G) :
    Tendsto (fun s : Finset Î± â†¦ âˆ‘' a : { x // x âˆ‰ s }, f a) atTop (ğ“ 0) := by
  classical
  by_cases H : Summable f
  Â· intro e he
    obtain âŸ¨s, hsâŸ© := H.tsum_vanishing he
    rw [Filter.mem_map, mem_atTop_sets]
    exact âŸ¨s, fun t hts â†¦ hs _ <| Set.disjoint_left.mpr fun a ha has â†¦ ha (hts has)âŸ©
  Â· convert tendsto_const_nhds (Î± := G) (Î² := Finset Î±) (f := atTop) (a := 0)
    apply tsum_eq_zero_of_not_summable
    rwa [Finset.summable_compl_iff]
#align tendsto_tsum_compl_at_top_zero tendsto_tsum_compl_atTop_zero

theorem Summable.nat_tsum_vanishing {f : â„• â†’ G} (hf : Summable f) â¦ƒe : Set Gâ¦„ (he : e âˆˆ ğ“ 0) :
    âˆƒ N : â„•, âˆ€ t âŠ† {n | N â‰¤ n}, (âˆ‘' n : t, f n) âˆˆ e :=
  letI : UniformSpace G := TopologicalAddGroup.toUniformSpace G
  have : UniformAddGroup G := comm_topologicalAddGroup_is_uniform
  cauchySeq_finset_iff_nat_tsum_vanishing.1 hf.hasSum.cauchySeq e he

/-- Series divergence test: if `f` is a convergent series, then `f x` tends to zero along
`cofinite`. -/
theorem Summable.tendsto_cofinite_zero (hf : Summable f) : Tendsto f cofinite (ğ“ 0) := by
  intro e he
  rw [Filter.mem_map]
  rcases hf.vanishing he with âŸ¨s, hsâŸ©
  refine' s.eventually_cofinite_nmem.mono fun x hx => _
  Â· simpa using hs {x} (disjoint_singleton_left.2 hx)
#align summable.tendsto_cofinite_zero Summable.tendsto_cofinite_zero

theorem Summable.tendsto_atTop_zero {f : â„• â†’ G} (hf : Summable f) : Tendsto f atTop (ğ“ 0) := by
  rw [â† Nat.cofinite_eq_atTop]
  exact hf.tendsto_cofinite_zero
#align summable.tendsto_at_top_zero Summable.tendsto_atTop_zero

theorem Summable.countable_support [FirstCountableTopology G] [T1Space G]
    (hf : Summable f) : f.support.Countable := by
  simpa only [ker_nhds] using hf.tendsto_cofinite_zero.countable_compl_preimage_ker

theorem summable_const_iff [Infinite Î²] [T2Space G] (a : G) :
    Summable (fun _ : Î² â†¦ a) â†” a = 0 := by
  refine âŸ¨fun h â†¦ ?_, ?_âŸ©
  Â· by_contra ha
    have : {a}á¶œ âˆˆ ğ“ 0 := compl_singleton_mem_nhds (Ne.symm ha)
    have : Finite Î² := by
      simpa [â† Set.finite_univ_iff] using h.tendsto_cofinite_zero this
    exact not_finite Î²
  Â· rintro rfl
    exact summable_zero

@[simp]
theorem tsum_const [T2Space G] : âˆ‘' _ : Î², (a : G) = Nat.card Î² â€¢ a := by
  rcases finite_or_infinite Î² with hÎ²|hÎ²
  Â· letI : Fintype Î² := Fintype.ofFinite Î²
    rw [tsum_eq_sum (s := univ) (fun x hx â†¦ (hx (mem_univ x)).elim)]
    simp only [sum_const, Nat.card_eq_fintype_card]
    rfl
  Â· simp only [Nat.card_eq_zero_of_infinite, zero_smul]
    rcases eq_or_ne a 0 with rfl|ha
    Â· simp
    Â· apply tsum_eq_zero_of_not_summable
      simpa [summable_const_iff] using ha

end TopologicalGroup

section ConstSMul

variable [Monoid Î³] [TopologicalSpace Î±] [AddCommMonoid Î±] [DistribMulAction Î³ Î±]
  [ContinuousConstSMul Î³ Î±] {f : Î² â†’ Î±}

theorem HasSum.const_smul {a : Î±} (b : Î³) (hf : HasSum f a) : HasSum (fun i => b â€¢ f i) (b â€¢ a) :=
  hf.map (DistribMulAction.toAddMonoidHom Î± _) <| continuous_const_smul _
#align has_sum.const_smul HasSum.const_smul

theorem Summable.const_smul (b : Î³) (hf : Summable f) : Summable fun i => b â€¢ f i :=
  (hf.hasSum.const_smul _).summable
#align summable.const_smul Summable.const_smul

/-- Infinite sums commute with scalar multiplication. Version for scalars living in a `Monoid`, but
  requiring a summability hypothesis. -/
theorem tsum_const_smul [T2Space Î±] (b : Î³) (hf : Summable f) : âˆ‘' i, b â€¢ f i = b â€¢ âˆ‘' i, f i :=
  (hf.hasSum.const_smul _).tsum_eq
#align tsum_const_smul tsum_const_smul

/-- Infinite sums commute with scalar multiplication. Version for scalars living in a `Group`, but
  not requiring any summability hypothesis. -/
lemma tsum_const_smul' {Î³ : Type*} [Group Î³] [DistribMulAction Î³ Î±] [ContinuousConstSMul Î³ Î±]
    [T2Space Î±] (g : Î³) : âˆ‘' (i : Î²), g â€¢ f i = g â€¢ âˆ‘' (i : Î²), f i := by
  by_cases hf : Summable f
  Â· exact tsum_const_smul g hf
  rw [tsum_eq_zero_of_not_summable hf]
  simp only [smul_zero]
  let mul_g : Î± â‰ƒ+ Î± := DistribMulAction.toAddEquiv Î± g
  apply tsum_eq_zero_of_not_summable
  change Â¬ Summable (mul_g âˆ˜ f)
  rwa [Summable.map_iff_of_equiv mul_g]
  Â· apply continuous_const_smul
  Â· apply continuous_const_smul

/-- Infinite sums commute with scalar multiplication. Version for scalars living in a
  `DivisionRing`; no summability hypothesis. This could be made to work for a
  `[GroupWithZero Î³]` if there was such a thing as `DistribMulActionWithZero`. -/
lemma tsum_const_smul'' {Î³ : Type*} [DivisionRing Î³] [Module Î³ Î±] [ContinuousConstSMul Î³ Î±]
    [T2Space Î±] (g : Î³) : âˆ‘' (i : Î²), g â€¢ f i = g â€¢ âˆ‘' (i : Î²), f i := by
  by_cases hf : Summable f
  Â· exact tsum_const_smul g hf
  rw [tsum_eq_zero_of_not_summable hf]
  simp only [smul_zero]
  by_cases hg : g = 0
  Â· simp [hg]
  let mul_g : Î± â‰ƒ+ Î± := DistribMulAction.toAddEquivâ‚€ Î± g hg
  apply tsum_eq_zero_of_not_summable
  change Â¬ Summable (mul_g âˆ˜ f)
  rwa [Summable.map_iff_of_equiv] <;> apply continuous_const_smul

end ConstSMul

/-! ### Product and pi types -/


section Prod

variable [AddCommMonoid Î±] [TopologicalSpace Î±] [AddCommMonoid Î³] [TopologicalSpace Î³]

theorem HasSum.prod_mk {f : Î² â†’ Î±} {g : Î² â†’ Î³} {a : Î±} {b : Î³} (hf : HasSum f a) (hg : HasSum g b) :
    HasSum (fun x => (âŸ¨f x, g xâŸ© : Î± Ã— Î³)) âŸ¨a, bâŸ© := by
  simp [HasSum, â† prod_mk_sum, Filter.Tendsto.prod_mk_nhds hf hg]
#align has_sum.prod_mk HasSum.prod_mk

end Prod

section Pi

variable {Î¹ : Type*} {Ï€ : Î± â†’ Type*} [âˆ€ x, AddCommMonoid (Ï€ x)] [âˆ€ x, TopologicalSpace (Ï€ x)]

theorem Pi.hasSum {f : Î¹ â†’ âˆ€ x, Ï€ x} {g : âˆ€ x, Ï€ x} :
    HasSum f g â†” âˆ€ x, HasSum (fun i => f i x) (g x) := by
  simp only [HasSum, tendsto_pi_nhds, sum_apply]
#align pi.has_sum Pi.hasSum

theorem Pi.summable {f : Î¹ â†’ âˆ€ x, Ï€ x} : Summable f â†” âˆ€ x, Summable fun i => f i x := by
  simp only [Summable, Pi.hasSum, Classical.skolem]
#align pi.summable Pi.summable

theorem tsum_apply [âˆ€ x, T2Space (Ï€ x)] {f : Î¹ â†’ âˆ€ x, Ï€ x} {x : Î±} (hf : Summable f) :
    (âˆ‘' i, f i) x = âˆ‘' i, f i x :=
  (Pi.hasSum.mp hf.hasSum x).tsum_eq.symm
#align tsum_apply tsum_apply

end Pi

/-! ### Multiplicative opposite -/


section MulOpposite

open MulOpposite

variable [AddCommMonoid Î±] [TopologicalSpace Î±] {f : Î² â†’ Î±} {a : Î±}

theorem HasSum.op (hf : HasSum f a) : HasSum (fun a => op (f a)) (op a) :=
  (hf.map (@opAddEquiv Î± _) continuous_op : _)
#align has_sum.op HasSum.op

theorem Summable.op (hf : Summable f) : Summable (op âˆ˜ f) :=
  hf.hasSum.op.summable
#align summable.op Summable.op

theorem HasSum.unop {f : Î² â†’ Î±áµáµ’áµ–} {a : Î±áµáµ’áµ–} (hf : HasSum f a) :
    HasSum (fun a => unop (f a)) (unop a) :=
  (hf.map (@opAddEquiv Î± _).symm continuous_unop : _)
#align has_sum.unop HasSum.unop

theorem Summable.unop {f : Î² â†’ Î±áµáµ’áµ–} (hf : Summable f) : Summable (unop âˆ˜ f) :=
  hf.hasSum.unop.summable
#align summable.unop Summable.unop

@[simp]
theorem hasSum_op : HasSum (fun a => op (f a)) (op a) â†” HasSum f a :=
  âŸ¨HasSum.unop, HasSum.opâŸ©
#align has_sum_op hasSum_op

@[simp]
theorem hasSum_unop {f : Î² â†’ Î±áµáµ’áµ–} {a : Î±áµáµ’áµ–} :
    HasSum (fun a => unop (f a)) (unop a) â†” HasSum f a :=
  âŸ¨HasSum.op, HasSum.unopâŸ©
#align has_sum_unop hasSum_unop

@[simp]
theorem summable_op : (Summable fun a => op (f a)) â†” Summable f :=
  âŸ¨Summable.unop, Summable.opâŸ©
#align summable_op summable_op

-- Porting note: This theorem causes a loop easily in Lean 4, so the priority should be `low`.
@[simp low]
theorem summable_unop {f : Î² â†’ Î±áµáµ’áµ–} : (Summable fun a => unop (f a)) â†” Summable f :=
  âŸ¨Summable.op, Summable.unopâŸ©
#align summable_unop summable_unop

variable [T2Space Î±]

theorem tsum_op : âˆ‘' x, MulOpposite.op (f x) = MulOpposite.op (âˆ‘' x, f x) := by
  by_cases h : Summable f
  Â· exact h.hasSum.op.tsum_eq
  Â· have ho := summable_op.not.mpr h
    rw [tsum_eq_zero_of_not_summable h, tsum_eq_zero_of_not_summable ho, MulOpposite.op_zero]
#align tsum_op tsum_op

theorem tsum_unop {f : Î² â†’ Î±áµáµ’áµ–} : âˆ‘' x, MulOpposite.unop (f x) = MulOpposite.unop (âˆ‘' x, f x) :=
  MulOpposite.op_injective tsum_op.symm
#align tsum_unop tsum_unop

end MulOpposite

/-! ### Interaction with the star -/


section ContinuousStar

variable [AddCommMonoid Î±] [TopologicalSpace Î±] [StarAddMonoid Î±] [ContinuousStar Î±] {f : Î² â†’ Î±}
  {a : Î±}

theorem HasSum.star (h : HasSum f a) : HasSum (fun b => star (f b)) (star a) := by
  simpa only using h.map (starAddEquiv : Î± â‰ƒ+ Î±) continuous_star
#align has_sum.star HasSum.star

theorem Summable.star (hf : Summable f) : Summable fun b => star (f b) :=
  hf.hasSum.star.summable
#align summable.star Summable.star

theorem Summable.ofStar (hf : Summable fun b => Star.star (f b)) : Summable f := by
  simpa only [star_star] using hf.star
#align summable.of_star Summable.ofStar

@[simp]
theorem summable_star_iff : (Summable fun b => star (f b)) â†” Summable f :=
  âŸ¨Summable.ofStar, Summable.starâŸ©
#align summable_star_iff summable_star_iff

@[simp]
theorem summable_star_iff' : Summable (star f) â†” Summable f :=
  summable_star_iff
#align summable_star_iff' summable_star_iff'

variable [T2Space Î±]

theorem tsum_star : star (âˆ‘' b, f b) = âˆ‘' b, star (f b) := by
  by_cases hf : Summable f
  Â· exact hf.hasSum.star.tsum_eq.symm
  Â· rw [tsum_eq_zero_of_not_summable hf, tsum_eq_zero_of_not_summable (mt Summable.ofStar hf),
      star_zero]
#align tsum_star tsum_star

end ContinuousStar

section automorphize

variable {M : Type*} [TopologicalSpace M] [AddCommMonoid M] [T2Space M] {R : Type*}
  [DivisionRing R] [Module R M] [ContinuousConstSMul R M]

/-- Given a group `Î±` acting on a type `Î²`, and a function `f : Î² â†’ M`, we "automorphize" `f` to a
  function `Î² â§¸ Î± â†’ M` by summing over `Î±` orbits, `b â†¦ âˆ‘' (a : Î±), f(a â€¢ b)`. -/
@[to_additive "Given an additive group `Î±` acting on a type `Î²`, and a function `f : Î² â†’ M`,
  we automorphize `f` to a function `Î² â§¸ Î± â†’ M` by summing over `Î±` orbits,
  `b â†¦ âˆ‘' (a : Î±), f(a â€¢ b)`."]
noncomputable def MulAction.automorphize [Group Î±] [MulAction Î± Î²] (f : Î² â†’ M) :
    Quotient (MulAction.orbitRel Î± Î²) â†’ M := by
  refine @Quotient.lift _ _ (_) (fun b â†¦ âˆ‘' (a : Î±), f (a â€¢ b)) ?_
  intro bâ‚ bâ‚‚ âŸ¨a, (ha : a â€¢ bâ‚‚ = bâ‚)âŸ©
  simp only
  rw [â† ha]
  convert (Equiv.mulRight a).tsum_eq (fun a' â†¦ f (a' â€¢ bâ‚‚)) using 1
  simp only [Equiv.coe_mulRight]
  congr
  ext
  congr 1
  simp only [mul_smul]

/-- Automorphization of a function into an `R`-`module` distributes, that is, commutes with the
`R`-scalar multiplication. -/
lemma MulAction.automorphize_smul_left [Group Î±] [MulAction Î± Î²] (f : Î² â†’ M)
    (g : Quotient (MulAction.orbitRel Î± Î²) â†’ R) :
    MulAction.automorphize ((g âˆ˜ (@Quotient.mk' _ (_))) â€¢ f)
      = g â€¢ (MulAction.automorphize f : Quotient (MulAction.orbitRel Î± Î²) â†’ M) := by
  ext x
  apply @Quotient.inductionOn' Î² (MulAction.orbitRel Î± Î²) _ x _
  intro b
  simp only [automorphize, Pi.smul_apply', comp_apply]
  set Ï€ : Î² â†’ Quotient (MulAction.orbitRel Î± Î²) := Quotient.mk (MulAction.orbitRel Î± Î²)
  have Hâ‚ : âˆ€ a : Î±, Ï€ (a â€¢ b) = Ï€ b
  Â· intro a
    apply (@Quotient.eq _ (MulAction.orbitRel Î± Î²) (a â€¢ b) b).mpr
    use a
  change âˆ‘' a : Î±, g (Ï€ (a â€¢ b)) â€¢ f (a â€¢ b) = g (Ï€ b) â€¢ âˆ‘' a : Î±, f (a â€¢ b)
  simp_rw [Hâ‚]
  exact tsum_const_smul'' _

/-- Automorphization of a function into an `R`-`module` distributes, that is, commutes with the
`R`-scalar multiplication. -/
lemma AddAction.automorphize_smul_left [AddGroup Î±] [AddAction Î± Î²]  (f : Î² â†’ M)
    (g : Quotient (AddAction.orbitRel Î± Î²) â†’ R) :
    AddAction.automorphize ((g âˆ˜ (@Quotient.mk' _ (_))) â€¢ f)
      = g â€¢ (AddAction.automorphize f : Quotient (AddAction.orbitRel Î± Î²) â†’ M) := by
  ext x
  apply @Quotient.inductionOn' Î² (AddAction.orbitRel Î± Î²) _ x _
  intro b
  simp only [automorphize, Pi.smul_apply', comp_apply]
  set Ï€ : Î² â†’ Quotient (AddAction.orbitRel Î± Î²) := Quotient.mk (AddAction.orbitRel Î± Î²)
  have Hâ‚ : âˆ€ a : Î±, Ï€ (a +áµ¥ b) = Ï€ b
  Â· intro a
    apply (@Quotient.eq _ (AddAction.orbitRel Î± Î²) (a +áµ¥ b) b).mpr
    use a
  change âˆ‘' a : Î±, g (Ï€ (a +áµ¥ b)) â€¢ f (a +áµ¥ b) = g (Ï€ b) â€¢ âˆ‘' a : Î±, f (a +áµ¥ b)
  simp_rw [Hâ‚]
  exact tsum_const_smul'' _

attribute [to_additive existing MulAction.automorphize_smul_left] AddAction.automorphize_smul_left

section

variable {G : Type*} [Group G] {Î“ : Subgroup G}

/-- Given a subgroup `Î“` of a group `G`, and a function `f : G â†’ M`, we "automorphize" `f` to a
  function `G â§¸ Î“ â†’ M` by summing over `Î“` orbits, `g â†¦ âˆ‘' (Î³ : Î“), f(Î³ â€¢ g)`. -/
@[to_additive "Given a subgroup `Î“` of an additive group `G`, and a function `f : G â†’ M`, we
  automorphize `f` to a function `G â§¸ Î“ â†’ M` by summing over `Î“` orbits,
  `g â†¦ âˆ‘' (Î³ : Î“), f(Î³ â€¢ g)`."]
noncomputable def QuotientGroup.automorphize  (f : G â†’ M) : G â§¸ Î“ â†’ M := MulAction.automorphize f

/-- Automorphization of a function into an `R`-`module` distributes, that is, commutes with the
`R`-scalar multiplication. -/
lemma QuotientGroup.automorphize_smul_left (f : G â†’ M) (g : G â§¸ Î“ â†’ R) :
    (QuotientGroup.automorphize ((g âˆ˜ (@Quotient.mk' _ (_)) : G â†’ R) â€¢ f) : G â§¸ Î“ â†’ M)
      = g â€¢ (QuotientGroup.automorphize f : G â§¸ Î“ â†’ M) :=
  MulAction.automorphize_smul_left f g

end

section

variable {G : Type*} [AddGroup G] {Î“ : AddSubgroup G}

/-- Automorphization of a function into an `R`-`module` distributes, that is, commutes with the
`R`-scalar multiplication. -/
lemma QuotientAddGroup.automorphize_smul_left (f : G â†’ M) (g : G â§¸ Î“ â†’ R) :
    QuotientAddGroup.automorphize ((g âˆ˜ (@Quotient.mk' _ (_))) â€¢ f)
      = g â€¢ (QuotientAddGroup.automorphize f : G â§¸ Î“ â†’ M) :=
  AddAction.automorphize_smul_left f g

end

attribute [to_additive existing QuotientGroup.automorphize_smul_left]
  QuotientAddGroup.automorphize_smul_left

end automorphize
