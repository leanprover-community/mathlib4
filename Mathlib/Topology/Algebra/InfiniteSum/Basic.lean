/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl
-/
import Mathlib.Topology.Algebra.InfiniteSum.Defs
import Mathlib.Data.Fintype.BigOperators
import Mathlib.Topology.Algebra.Monoid

/-!
# Lemmas on infinite sums and products in topological monoids

This file contains many simple lemmas on `tsum`, `HasSum` etc, which are placed here in order to
keep the basic file of definitions as short as possible.

Results requiring a group (rather than monoid) structure on the target should go in `Group.lean`.
-/

noncomputable section

open Filter Finset Function

open scoped BigOperators Topology

variable {Œ± Œ≤ Œ≥ Œ¥ : Type*}

section HasProd

variable [CommMonoid Œ±] [TopologicalSpace Œ±]
variable {f g : Œ≤ ‚Üí Œ±} {a b : Œ±} {s : Finset Œ≤}

/-- Constant one function has product `1` -/
@[to_additive "Constant zero function has sum `0`"]
theorem hasProd_one : HasProd (fun _ ‚Ü¶ 1 : Œ≤ ‚Üí Œ±) 1 := by simp [HasProd, tendsto_const_nhds]
#align has_sum_zero hasSum_zero

@[to_additive]
theorem hasProd_empty [IsEmpty Œ≤] : HasProd f 1 := by
  convert @hasProd_one Œ± Œ≤ _ _
#align has_sum_empty hasSum_empty

@[to_additive]
theorem prodable_one : Prodable (fun _ ‚Ü¶ 1 : Œ≤ ‚Üí Œ±) :=
  hasProd_one.prodable
#align summable_zero summable_zero

@[to_additive]
theorem prodable_empty [IsEmpty Œ≤] : Prodable f :=
  hasProd_empty.prodable
#align summable_empty summable_empty

@[to_additive]
theorem prodable_congr (hfg : ‚àÄ b, f b = g b) : Prodable f ‚Üî Prodable g :=
  iff_of_eq (congr_arg Prodable <| funext hfg)
#align summable_congr summable_congr

@[to_additive]
theorem Prodable.congr (hf : Prodable f) (hfg : ‚àÄ b, f b = g b) : Prodable g :=
  (prodable_congr hfg).mp hf
#align summable.congr Summable.congr

@[to_additive]
theorem HasProd.hasProd_of_prod_eq {g : Œ≥ ‚Üí Œ±}
    (h_eq : ‚àÄ u : Finset Œ≥, ‚àÉ v : Finset Œ≤, ‚àÄ v', v ‚äÜ v' ‚Üí
      ‚àÉ u', u ‚äÜ u' ‚àß ‚àè x in u', g x = ‚àè b in v', f b)
    (hf : HasProd g a) : HasProd f a :=
  le_trans (map_atTop_finset_prod_le_of_prod_eq h_eq) hf
#align has_sum.has_sum_of_sum_eq HasSum.hasSum_of_sum_eq

@[to_additive]
theorem hasProd_iff_hasProd {g : Œ≥ ‚Üí Œ±}
    (h‚ÇÅ : ‚àÄ u : Finset Œ≥, ‚àÉ v : Finset Œ≤, ‚àÄ v', v ‚äÜ v' ‚Üí
      ‚àÉ u', u ‚äÜ u' ‚àß ‚àè x in u', g x = ‚àè b in v', f b)
    (h‚ÇÇ : ‚àÄ v : Finset Œ≤, ‚àÉ u : Finset Œ≥, ‚àÄ u', u ‚äÜ u' ‚Üí
      ‚àÉ v', v ‚äÜ v' ‚àß ‚àè b in v', f b = ‚àè x in u', g x) :
    HasProd f a ‚Üî HasProd g a :=
  ‚ü®HasProd.hasProd_of_prod_eq h‚ÇÇ, HasProd.hasProd_of_prod_eq h‚ÇÅ‚ü©
#align has_sum_iff_has_sum hasSum_iff_hasSum

@[to_additive]
theorem Function.Injective.prodable_iff {g : Œ≥ ‚Üí Œ≤} (hg : Injective g)
    (hf : ‚àÄ x ‚àâ Set.range g, f x = 1) : Prodable (f ‚àò g) ‚Üî Prodable f :=
  exists_congr fun _ ‚Ü¶ hg.hasProd_iff hf
#align function.injective.summable_iff Function.Injective.summable_iff

@[to_additive (attr := simp)] theorem hasProd_extend_one {g : Œ≤ ‚Üí Œ≥} (hg : Injective g) :
    HasProd (extend g f 1) a ‚Üî HasProd f a := by
  rw [‚Üê hg.hasProd_iff, extend_comp hg]
  exact extend_apply' _ _

@[to_additive (attr := simp)] theorem prodable_extend_one {g : Œ≤ ‚Üí Œ≥} (hg : Injective g) :
    Prodable (extend g f 1) ‚Üî Prodable f :=
  exists_congr fun _ ‚Ü¶ hasProd_extend_one hg

@[to_additive]
theorem hasProd_subtype_iff_mulIndicator {s : Set Œ≤} :
    HasProd (f ‚àò (‚Üë) : s ‚Üí Œ±) a ‚Üî HasProd (s.mulIndicator f) a := by
  rw [‚Üê Set.mulIndicator_range_comp, Subtype.range_coe,
    hasProd_subtype_iff_of_mulSupport_subset Set.mulSupport_mulIndicator_subset]
#align has_sum_subtype_iff_indicator hasSum_subtype_iff_indicator

@[to_additive]
theorem prodable_subtype_iff_mulIndicator {s : Set Œ≤} :
    Prodable (f ‚àò (‚Üë) : s ‚Üí Œ±) ‚Üî Prodable (s.mulIndicator f) :=
  exists_congr fun _ ‚Ü¶ hasProd_subtype_iff_mulIndicator
#align summable_subtype_iff_indicator summable_subtype_iff_indicator

@[to_additive (attr := simp)]
theorem hasProd_subtype_mulSupport : HasProd (f ‚àò (‚Üë) : mulSupport f ‚Üí Œ±) a ‚Üî HasProd f a :=
  hasProd_subtype_iff_of_mulSupport_subset <| Set.Subset.refl _
#align has_sum_subtype_support hasSum_subtype_support

@[to_additive]
protected theorem Finset.prodable (s : Finset Œ≤) (f : Œ≤ ‚Üí Œ±) :
    Prodable (f ‚àò (‚Üë) : (‚Üës : Set Œ≤) ‚Üí Œ±) :=
  (s.hasProd f).prodable
#align finset.summable Finset.summable

@[to_additive]
protected theorem Set.Finite.prodable {s : Set Œ≤} (hs : s.Finite) (f : Œ≤ ‚Üí Œ±) :
    Prodable (f ‚àò (‚Üë) : s ‚Üí Œ±) := by
  have := hs.toFinset.prodable f
  rwa [hs.coe_toFinset] at this
#align set.finite.summable Set.Finite.summable

@[to_additive]
theorem prodable_of_finite_mulSupport (h : (mulSupport f).Finite) : Prodable f := by
  apply prodable_of_ne_finset_one (s := h.toFinset); simp

@[to_additive]
theorem hasProd_single {f : Œ≤ ‚Üí Œ±} (b : Œ≤) (hf : ‚àÄ (b') (_ : b' ‚â† b), f b' = 1) : HasProd f (f b) :=
  suffices HasProd f (‚àè b' in {b}, f b') by simpa using this
  hasProd_prod_of_ne_finset_one <| by simpa [hf]
#align has_sum_single hasSum_single

@[to_additive (attr := simp)] lemma hasProd_unique [Unique Œ≤] (f : Œ≤ ‚Üí Œ±) : HasProd f (f default) :=
  hasProd_single default (fun _ hb ‚Ü¶ False.elim <| hb <| Unique.uniq ..)

@[to_additive (attr := simp)]
lemma hasProd_singleton (m : Œ≤) (f : Œ≤ ‚Üí Œ±) : HasProd (({m} : Set Œ≤).restrict f) (f m) :=
  hasProd_unique (Set.restrict {m} f)

@[to_additive]
theorem hasProd_ite_eq (b : Œ≤) [DecidablePred (¬∑ = b)] (a : Œ±) :
    HasProd (fun b' ‚Ü¶ if b' = b then a else 1) a := by
  convert @hasProd_single _ _ _ _ (fun b' ‚Ü¶ if b' = b then a else 1) b (fun b' hb' ‚Ü¶ if_neg hb')
  exact (if_pos rfl).symm
#align has_sum_ite_eq hasSum_ite_eq

@[to_additive]
theorem Equiv.hasProd_iff (e : Œ≥ ‚âÉ Œ≤) : HasProd (f ‚àò e) a ‚Üî HasProd f a :=
  e.injective.hasProd_iff <| by simp
#align equiv.has_sum_iff Equiv.hasSum_iff

@[to_additive]
theorem Function.Injective.hasProd_range_iff {g : Œ≥ ‚Üí Œ≤} (hg : Injective g) :
    HasProd (fun x : Set.range g ‚Ü¶ f x) a ‚Üî HasProd (f ‚àò g) a :=
  (Equiv.ofInjective g hg).hasProd_iff.symm
#align function.injective.has_sum_range_iff Function.Injective.hasSum_range_iff

@[to_additive]
theorem Equiv.prodable_iff (e : Œ≥ ‚âÉ Œ≤) : Prodable (f ‚àò e) ‚Üî Prodable f :=
  exists_congr fun _ ‚Ü¶ e.hasProd_iff
#align equiv.summable_iff Equiv.summable_iff

@[to_additive]
theorem Equiv.hasProd_iff_of_mulSupport {g : Œ≥ ‚Üí Œ±} (e : mulSupport f ‚âÉ mulSupport g)
    (he : ‚àÄ x : mulSupport f, g (e x) = f x) : HasProd f a ‚Üî HasProd g a := by
  have : (g ‚àò (‚Üë)) ‚àò e = f ‚àò (‚Üë) := funext he
  rw [‚Üê hasProd_subtype_mulSupport, ‚Üê this, e.hasProd_iff, hasProd_subtype_mulSupport]
#align equiv.has_sum_iff_of_support Equiv.hasSum_iff_of_support

@[to_additive]
theorem hasProd_iff_hasProd_of_ne_one_bij {g : Œ≥ ‚Üí Œ±} (i : mulSupport g ‚Üí Œ≤)
    (hi : Injective i) (hf : mulSupport f ‚äÜ Set.range i)
    (hfg : ‚àÄ x, f (i x) = g x) : HasProd f a ‚Üî HasProd g a :=
  Iff.symm <|
    Equiv.hasProd_iff_of_mulSupport
      (Equiv.ofBijective (fun x ‚Ü¶ ‚ü®i x, fun hx ‚Ü¶ x.coe_prop <| hfg x ‚ñ∏ hx‚ü©)
        ‚ü®fun _ _ h ‚Ü¶ hi <| Subtype.ext_iff.1 h, fun y ‚Ü¶
          (hf y.coe_prop).imp fun _ hx ‚Ü¶ Subtype.ext hx‚ü©)
      hfg
#align has_sum_iff_has_sum_of_ne_zero_bij hasSum_iff_hasSum_of_ne_zero_bij

@[to_additive]
theorem Equiv.prodable_iff_of_mulSupport {g : Œ≥ ‚Üí Œ±} (e : mulSupport f ‚âÉ mulSupport g)
    (he : ‚àÄ x : mulSupport f, g (e x) = f x) : Prodable f ‚Üî Prodable g :=
  exists_congr fun _ ‚Ü¶ e.hasProd_iff_of_mulSupport he
#align equiv.summable_iff_of_support Equiv.summable_iff_of_support

@[to_additive]
protected theorem HasProd.map [CommMonoid Œ≥] [TopologicalSpace Œ≥] (hf : HasProd f a) {G}
    [FunLike G Œ± Œ≥] [MonoidHomClass G Œ± Œ≥] (g : G) (hg : Continuous g) :
    HasProd (g ‚àò f) (g a) :=
  have : (g ‚àò fun s : Finset Œ≤ ‚Ü¶ ‚àè b in s, f b) = fun s : Finset Œ≤ ‚Ü¶ ‚àè b in s, g (f b) :=
    funext <| map_prod g _
  show Tendsto (fun s : Finset Œ≤ ‚Ü¶ ‚àè b in s, g (f b)) atTop (ùìù (g a)) from
    this ‚ñ∏ (hg.tendsto a).comp hf
#align has_sum.map HasSum.map

@[to_additive]
protected theorem Prodable.map [CommMonoid Œ≥] [TopologicalSpace Œ≥] (hf : Prodable f) {G}
    [FunLike G Œ± Œ≥] [MonoidHomClass G Œ± Œ≥] (g : G) (hg : Continuous g) : Prodable (g ‚àò f) :=
  (hf.hasProd.map g hg).prodable
#align summable.map Summable.map

@[to_additive]
protected theorem Prodable.map_iff_of_leftInverse [CommMonoid Œ≥] [TopologicalSpace Œ≥] {G G'}
    [FunLike G Œ± Œ≥] [MonoidHomClass G Œ± Œ≥] [FunLike G' Œ≥ Œ±] [MonoidHomClass G' Œ≥ Œ±]
    (g : G) (g' : G') (hg : Continuous g)
    (hg' : Continuous g') (hinv : Function.LeftInverse g' g) : Prodable (g ‚àò f) ‚Üî Prodable f :=
  ‚ü®fun h ‚Ü¶ by
    have := h.map _ hg'
    rwa [‚Üê Function.comp.assoc, hinv.id] at this, fun h ‚Ü¶ h.map _ hg‚ü©
#align summable.map_iff_of_left_inverse Summable.map_iff_of_leftInverse

/-- "A special case of `Prodable.map_iff_of_leftInverse` for convenience" -/
@[to_additive "A special case of `Summable.map_iff_of_leftInverse` for convenience"]
protected theorem Prodable.map_iff_of_equiv [CommMonoid Œ≥] [TopologicalSpace Œ≥] {G}
    [EquivLike G Œ± Œ≥] [MulEquivClass G Œ± Œ≥] (g : G) (hg : Continuous g)
    (hg' : Continuous (EquivLike.inv g : Œ≥ ‚Üí Œ±)) : Prodable (g ‚àò f) ‚Üî Prodable f :=
  Prodable.map_iff_of_leftInverse g (g : Œ± ‚âÉ* Œ≥).symm hg hg' (EquivLike.left_inv g)
#align summable.map_iff_of_equiv Summable.map_iff_of_equiv

@[to_additive]
theorem Function.Surjective.prodable_iff_of_hasProd_iff {Œ±' : Type*} [CommMonoid Œ±']
    [TopologicalSpace Œ±'] {e : Œ±' ‚Üí Œ±} (hes : Function.Surjective e) {f : Œ≤ ‚Üí Œ±} {g : Œ≥ ‚Üí Œ±'}
    (he : ‚àÄ {a}, HasProd f (e a) ‚Üî HasProd g a) : Prodable f ‚Üî Prodable g :=
  hes.exists.trans <| exists_congr <| @he
#align function.surjective.summable_iff_of_has_sum_iff Function.Surjective.summable_iff_of_hasSum_iff

variable [ContinuousMul Œ±]

@[to_additive]
theorem HasProd.mul (hf : HasProd f a) (hg : HasProd g b) :
    HasProd (fun b ‚Ü¶ f b * g b) (a * b) := by
  dsimp only [HasProd] at hf hg ‚ä¢
  simp_rw [prod_mul_distrib]
  exact hf.mul hg
#align has_sum.add HasSum.add

@[to_additive]
theorem Prodable.mul (hf : Prodable f) (hg : Prodable g) : Prodable fun b ‚Ü¶ f b * g b :=
  (hf.hasProd.mul hg.hasProd).prodable
#align summable.add Summable.add

@[to_additive]
theorem hasProd_prod {f : Œ≥ ‚Üí Œ≤ ‚Üí Œ±} {a : Œ≥ ‚Üí Œ±} {s : Finset Œ≥} :
    (‚àÄ i ‚àà s, HasProd (f i) (a i)) ‚Üí HasProd (fun b ‚Ü¶ ‚àè i in s, f i b) (‚àè i in s, a i) := by
  classical
  exact Finset.induction_on s (by simp only [hasProd_one, prod_empty, forall_true_iff]) <| by
    -- Porting note: with some help, `simp` used to be able to close the goal
    simp (config := { contextual := true }) only [mem_insert, forall_eq_or_imp, not_false_iff,
      prod_insert, and_imp]
    exact fun x s _ IH hx h ‚Ü¶ hx.mul (IH h)
#align has_sum_sum hasSum_sum

@[to_additive]
theorem prodable_prod {f : Œ≥ ‚Üí Œ≤ ‚Üí Œ±} {s : Finset Œ≥} (hf : ‚àÄ i ‚àà s, Prodable (f i)) :
    Prodable fun b ‚Ü¶ ‚àè i in s, f i b :=
  (hasProd_prod fun i hi ‚Ü¶ (hf i hi).hasProd).prodable
#align summable_sum summable_sum

@[to_additive]
theorem HasProd.mul_disjoint {s t : Set Œ≤} (hs : Disjoint s t) (ha : HasProd (f ‚àò (‚Üë) : s ‚Üí Œ±) a)
    (hb : HasProd (f ‚àò (‚Üë) : t ‚Üí Œ±) b) : HasProd (f ‚àò (‚Üë) : (s ‚à™ t : Set Œ≤) ‚Üí Œ±) (a * b) := by
  rw [hasProd_subtype_iff_mulIndicator] at *
  rw [Set.mulIndicator_union_of_disjoint hs]
  exact ha.mul hb
#align has_sum.add_disjoint HasSum.add_disjoint

@[to_additive]
theorem hasProd_prod_disjoint {Œπ} (s : Finset Œπ) {t : Œπ ‚Üí Set Œ≤} {a : Œπ ‚Üí Œ±}
    (hs : (s : Set Œπ).Pairwise (Disjoint on t)) (hf : ‚àÄ i ‚àà s, HasProd (f ‚àò (‚Üë) : t i ‚Üí Œ±) (a i)) :
    HasProd (f ‚àò (‚Üë) : (‚ãÉ i ‚àà s, t i) ‚Üí Œ±) (‚àè i in s, a i) := by
  simp_rw [hasProd_subtype_iff_mulIndicator] at *
  rw [Finset.mulIndicator_biUnion _ _ hs]
  exact hasProd_prod hf
#align has_sum_sum_disjoint hasSum_sum_disjoint

@[to_additive]
theorem HasProd.mul_isCompl {s t : Set Œ≤} (hs : IsCompl s t) (ha : HasProd (f ‚àò (‚Üë) : s ‚Üí Œ±) a)
    (hb : HasProd (f ‚àò (‚Üë) : t ‚Üí Œ±) b) : HasProd f (a * b) := by
  simpa [‚Üê hs.compl_eq] using
    (hasProd_subtype_iff_mulIndicator.1 ha).mul (hasProd_subtype_iff_mulIndicator.1 hb)
#align has_sum.add_is_compl HasSum.add_isCompl

@[to_additive]
theorem HasProd.mul_compl {s : Set Œ≤} (ha : HasProd (f ‚àò (‚Üë) : s ‚Üí Œ±) a)
    (hb : HasProd (f ‚àò (‚Üë) : (s·∂ú : Set Œ≤) ‚Üí Œ±) b) : HasProd f (a * b) :=
  ha.mul_isCompl isCompl_compl hb
#align has_sum.add_compl HasSum.add_compl

@[to_additive]
theorem Prodable.mul_compl {s : Set Œ≤} (hs : Prodable (f ‚àò (‚Üë) : s ‚Üí Œ±))
    (hsc : Prodable (f ‚àò (‚Üë) : (s·∂ú : Set Œ≤) ‚Üí Œ±)) : Prodable f :=
  (hs.hasProd.mul_compl hsc.hasProd).prodable
#align summable.add_compl Summable.add_compl

@[to_additive]
theorem HasProd.compl_mul {s : Set Œ≤} (ha : HasProd (f ‚àò (‚Üë) : (s·∂ú : Set Œ≤) ‚Üí Œ±) a)
    (hb : HasProd (f ‚àò (‚Üë) : s ‚Üí Œ±) b) : HasProd f (a * b) :=
  ha.mul_isCompl isCompl_compl.symm hb
#align has_sum.compl_add HasSum.compl_add

@[to_additive]
theorem Prodable.compl_add {s : Set Œ≤} (hs : Prodable (f ‚àò (‚Üë) : (s·∂ú : Set Œ≤) ‚Üí Œ±))
    (hsc : Prodable (f ‚àò (‚Üë) : s ‚Üí Œ±)) : Prodable f :=
  (hs.hasProd.compl_mul hsc.hasProd).prodable
#align summable.compl_add Summable.compl_add

@[to_additive "Version of `HasSum.update` for `AddCommMonoid` rather than `AddCommGroup`.
Rather than showing that `f.update` has a specific sum in terms of `HasSum`,
it gives a relationship between the sums of `f` and `f.update` given that both exist."]
theorem HasProd.update' {Œ± Œ≤ : Type*} [TopologicalSpace Œ±] [CommMonoid Œ±] [T2Space Œ±]
    [ContinuousMul Œ±] [DecidableEq Œ≤] {f : Œ≤ ‚Üí Œ±} {a a' : Œ±} (hf : HasProd f a) (b : Œ≤) (x : Œ±)
    (hf' : HasProd (update f b x) a') : a * x = a' * f b := by
  have : ‚àÄ b', f b' * ite (b' = b) x 1 = update f b x b' * ite (b' = b) (f b) 1 := by
    intro b'
    split_ifs with hb'
    ¬∑ simpa only [Function.update_apply, hb', eq_self_iff_true] using mul_comm (f b) x
    ¬∑ simp only [Function.update_apply, hb', if_false]
  have h := hf.mul (hasProd_ite_eq b x)
  simp_rw [this] at h
  exact HasProd.unique h (hf'.mul (hasProd_ite_eq b (f b)))
#align has_sum.update' HasSum.update'

@[to_additive "Version of `hasSum_ite_sub_hasSum` for `AddCommMonoid` rather than `AddCommGroup`.
Rather than showing that the `ite` expression has a specific sum in terms of `HasSum`,
it gives a relationship between the sums of `f` and `ite (n = b) 0 (f n)` given that both exist."]
theorem eq_mul_of_hasProd_ite {Œ± Œ≤ : Type*} [TopologicalSpace Œ±] [CommMonoid Œ±] [T2Space Œ±]
    [ContinuousMul Œ±] [DecidableEq Œ≤] {f : Œ≤ ‚Üí Œ±} {a : Œ±} (hf : HasProd f a) (b : Œ≤) (a' : Œ±)
    (hf' : HasProd (fun n ‚Ü¶ ite (n = b) 1 (f n)) a') : a = a' * f b := by
  refine' (mul_one a).symm.trans (hf.update' b 1 _)
  convert hf'
  apply update_apply
#align eq_add_of_has_sum_ite eq_add_of_hasSum_ite

end HasProd

section tprod

variable [CommMonoid Œ±] [TopologicalSpace Œ±] {f g : Œ≤ ‚Üí Œ±} {a a‚ÇÅ a‚ÇÇ : Œ±}

@[to_additive]
theorem tprod_congr_set_coe (f : Œ≤ ‚Üí Œ±) {s t : Set Œ≤} (h : s = t) :
    ‚àè' x : s, f x = ‚àè' x : t, f x := by rw [h]
#align tsum_congr_subtype tsum_congr_set_coe

@[to_additive]
theorem tprod_congr_subtype (f : Œ≤ ‚Üí Œ±) {P Q : Œ≤ ‚Üí Prop} (h : ‚àÄ x, P x ‚Üî Q x):
    ‚àè' x : {x // P x}, f x = ‚àè' x : {x // Q x}, f x :=
  tprod_congr_set_coe f <| Set.ext h

@[to_additive]
theorem tprod_eq_finprod (hf : (mulSupport f).Finite) :
    ‚àè' b, f b = ‚àè·∂† b, f b := by simp [tprod_def, prodable_of_finite_mulSupport hf, hf]

@[to_additive]
theorem tprod_eq_prod' {s : Finset Œ≤} (hf : mulSupport f ‚äÜ s) :
    ‚àè' b, f b = ‚àè b in s, f b := by
  rw [tprod_eq_finprod (s.finite_toSet.subset hf), finprod_eq_prod_of_mulSupport_subset _ hf]

@[to_additive]
theorem tprod_eq_prod {s : Finset Œ≤} (hf : ‚àÄ b ‚àâ s, f b = 1) :
    ‚àè' b, f b = ‚àè b in s, f b :=
  tprod_eq_prod' <| mulSupport_subset_iff'.2 hf
#align tsum_eq_sum tsum_eq_sum

@[to_additive (attr := simp)]
theorem tprod_zero : ‚àè' _ : Œ≤, (1 : Œ±) = 1 := by rw [tprod_eq_finprod] <;> simp
#align tsum_zero tsum_zero
#align tsum_zero' tsum_zero

@[to_additive (attr := simp)]
theorem tprod_empty [IsEmpty Œ≤] : ‚àè' b, f b = 1 := by
  rw [tprod_eq_prod (s := (‚àÖ : Finset Œ≤))] <;> simp
#align tsum_empty tsum_empty

@[to_additive]
theorem tprod_congr {f g : Œ≤ ‚Üí Œ±}
    (hfg : ‚àÄ b, f b = g b) : ‚àè' b, f b = ‚àè' b, g b :=
  congr_arg tprod (funext hfg)
#align tsum_congr tsum_congr

@[to_additive]
theorem tprod_fintype [Fintype Œ≤] (f : Œ≤ ‚Üí Œ±) : ‚àè' b, f b = ‚àè b, f b := by
  apply tprod_eq_prod; simp
#align tsum_fintype tsum_fintype

@[to_additive]
theorem prod_eq_tprod_mulIndicator (f : Œ≤ ‚Üí Œ±) (s : Finset Œ≤) :
    ‚àè x in s, f x = ‚àè' x, Set.mulIndicator (‚Üës) f x := by
  rw [tprod_eq_prod' (Set.mulSupport_mulIndicator_subset), Finset.prod_mulIndicator_subset _ Finset.Subset.rfl]
#align sum_eq_tsum_indicator sum_eq_tsum_indicator

@[to_additive]
theorem tprod_bool (f : Bool ‚Üí Œ±) : ‚àè' i : Bool, f i = f false * f true := by
  rw [tprod_fintype, Fintype.prod_bool, mul_comm]
#align tsum_bool tsum_bool

@[to_additive]
theorem tprod_eq_single {f : Œ≤ ‚Üí Œ±} (b : Œ≤) (hf : ‚àÄ b' ‚â† b, f b' = 1) :
    ‚àè' b, f b = f b := by
  rw [tprod_eq_prod (s := {b}), prod_singleton]
  exact fun b' hb' ‚Ü¶ hf b' (by simpa using hb')
#align tsum_eq_single tsum_eq_single

@[to_additive]
theorem tprod_tprod_eq_single (f : Œ≤ ‚Üí Œ≥ ‚Üí Œ±) (b : Œ≤) (c : Œ≥) (hfb : ‚àÄ b' ‚â† b, f b' c = 1)
    (hfc : ‚àÄ b', ‚àÄ c' ‚â† c, f b' c' = 1) : ‚àè' (b') (c'), f b' c' = f b c :=
  calc
    ‚àè' (b') (c'), f b' c' = ‚àè' b', f b' c := tprod_congr fun b' ‚Ü¶ tprod_eq_single _ (hfc b')
    _ = f b c := tprod_eq_single _ hfb
#align tsum_tsum_eq_single tsum_tsum_eq_single

@[to_additive (attr := simp)]
theorem tprod_ite_eq (b : Œ≤) [DecidablePred (¬∑ = b)] (a : Œ±) :
    ‚àè' b', (if b' = b then a else 1) = a := by
  rw [tprod_eq_single b]
  ¬∑ simp
  ¬∑ intro b' hb'; simp [hb']
#align tsum_ite_eq tsum_ite_eq

-- Porting note: Added nolint simpNF, simpNF falsely claims that lhs does not simplify under simp
@[to_additive (attr := simp, nolint simpNF)]
theorem Finset.tprod_subtype (s : Finset Œ≤) (f : Œ≤ ‚Üí Œ±) :
    ‚àè' x : { x // x ‚àà s }, f x = ‚àè x in s, f x := by
  rw [‚Üê prod_attach]; exact tprod_fintype _
#align finset.tsum_subtype Finset.tsum_subtype

@[to_additive]
theorem Finset.tprod_subtype' (s : Finset Œ≤) (f : Œ≤ ‚Üí Œ±) :
    ‚àè' x : (s : Set Œ≤), f x = ‚àè x in s, f x := by simp
#align finset.tsum_subtype' Finset.tsum_subtype'

-- Porting note: Added nolint simpNF, simpNF falsely claims that lhs does not simplify under simp
@[to_additive (attr := simp, nolint simpNF)]
theorem tprod_singleton (b : Œ≤) (f : Œ≤ ‚Üí Œ±) : ‚àè' x : ({b} : Set Œ≤), f x = f b := by
  rw [‚Üê coe_singleton, Finset.tprod_subtype', prod_singleton]
#align tsum_singleton tsum_singleton

open scoped Classical in
@[to_additive]
theorem Function.Injective.tprod_eq {g : Œ≥ ‚Üí Œ≤} (hg : Injective g) {f : Œ≤ ‚Üí Œ±}
    (hf : mulSupport f ‚äÜ Set.range g) : ‚àè' c, f (g c) = ‚àè' b, f b := by
  have : mulSupport f = g '' mulSupport (f ‚àò g) := by
    rw [mulSupport_comp_eq_preimage, Set.image_preimage_eq_iff.2 hf]
  rw [‚Üê Function.comp_def]
  by_cases hf_fin : (mulSupport f).Finite
  ¬∑ have hfg_fin : (mulSupport (f ‚àò g)).Finite := hf_fin.preimage (hg.injOn _)
    lift g to Œ≥ ‚Ü™ Œ≤ using hg
    simp_rw [tprod_eq_prod' hf_fin.coe_toFinset.ge, tprod_eq_prod' hfg_fin.coe_toFinset.ge,
      comp_apply, ‚Üê Finset.prod_map]
    refine Finset.prod_congr (Finset.coe_injective ?_) fun _ _ ‚Ü¶ rfl
    simp [this]
  ¬∑ have hf_fin' : ¬¨ Set.Finite (mulSupport (f ‚àò g)) := by
      rwa [this, Set.finite_image_iff (hg.injOn _)] at hf_fin
    simp_rw [tprod_def, if_neg hf_fin, if_neg hf_fin', Prodable,
      hg.hasProd_iff (mulSupport_subset_iff'.1 hf)]

@[to_additive]
theorem Equiv.tprod_eq (e : Œ≥ ‚âÉ Œ≤) (f : Œ≤ ‚Üí Œ±) : ‚àè' c, f (e c) = ‚àè' b, f b :=
  e.injective.tprod_eq <| by simp
#align equiv.tsum_eq Equiv.tsum_eq

/-! ### `tprod` on subsets - part 1 -/

@[to_additive]
theorem tprod_subtype_eq_of_mulSupport_subset {f : Œ≤ ‚Üí Œ±} {s : Set Œ≤} (hs : mulSupport f ‚äÜ s) :
    ‚àè' x : s, f x = ‚àè' x, f x :=
  Subtype.val_injective.tprod_eq <| by simpa
#align tsum_subtype_eq_of_support_subset tsum_subtype_eq_of_support_subset

@[to_additive]
theorem tprod_subtype_mulSupport (f : Œ≤ ‚Üí Œ±) : ‚àè' x : mulSupport f, f x = ‚àè' x, f x :=
  tprod_subtype_eq_of_mulSupport_subset Set.Subset.rfl

@[to_additive]
theorem tprod_subtype (s : Set Œ≤) (f : Œ≤ ‚Üí Œ±) : ‚àè' x : s, f x = ‚àè' x, s.mulIndicator f x := by
  rw [‚Üê tprod_subtype_eq_of_mulSupport_subset Set.mulSupport_mulIndicator_subset, tprod_congr]
  simp
#align tsum_subtype tsum_subtype

-- Porting note: Added nolint simpNF, simpNF falsely claims that lhs does not simplify under simp
@[to_additive (attr := simp, nolint simpNF)]
theorem tprod_univ (f : Œ≤ ‚Üí Œ±) : ‚àè' x : (Set.univ : Set Œ≤), f x = ‚àè' x, f x :=
  tprod_subtype_eq_of_mulSupport_subset <| Set.subset_univ _
#align tsum_univ tsum_univ

@[to_additive]
theorem tprod_image {g : Œ≥ ‚Üí Œ≤} (f : Œ≤ ‚Üí Œ±) {s : Set Œ≥} (hg : Set.InjOn g s) :
    ‚àè' x : g '' s, f x = ‚àè' x : s, f (g x) :=
  ((Equiv.Set.imageOfInjOn _ _ hg).tprod_eq fun x ‚Ü¶ f x).symm
#align tsum_image tsum_image

@[to_additive]
theorem tprod_range {g : Œ≥ ‚Üí Œ≤} (f : Œ≤ ‚Üí Œ±) (hg : Injective g) :
    ‚àè' x : Set.range g, f x = ‚àè' x, f (g x) := by
  rw [‚Üê Set.image_univ, tprod_image f (hg.injOn _)]
  simp_rw [‚Üê comp_apply (g := g), tprod_univ (f ‚àò g)]
#align tsum_range tsum_range

/-- If `f b = 1` for all `b ‚àà t`, then the product of `f a` with `a ‚àà s` is the same as the
product of `f a` with `a ‚àà s ‚àñ t`. -/
@[to_additive "If `f b = 0` for all `b ‚àà t`, then the sum of `f a` with `a ‚àà s` is the same as the
sum of `f a` with `a ‚àà s ‚àñ t`."]
lemma tprod_setElem_eq_tprod_setElem_diff {f : Œ≤ ‚Üí Œ±} (s t : Set Œ≤)
    (hf‚ÇÄ : ‚àÄ b ‚àà t, f b = 1) :
    ‚àè' a : s, f a = ‚àè' a : (s \ t : Set Œ≤), f a :=
  .symm <| (Set.inclusion_injective (Set.diff_subset s t)).tprod_eq (f := f ‚àò (‚Üë)) <|
    mulSupport_subset_iff'.2 fun b hb ‚Ü¶ hf‚ÇÄ b <| by simpa using hb

/-- If `f b = 1`, then the product of `f a` with `a ‚àà s` is the same as the product of `f a` for
`a ‚àà s ‚àñ {b}`. -/
@[to_additive "If `f b = 0`, then the sum of `f a` with `a ‚àà s` is the same as the sum of `f a`
for `a ‚àà s ‚àñ {b}`."]
lemma tprod_eq_tprod_diff_singleton {f : Œ≤ ‚Üí Œ±} (s : Set Œ≤) {b : Œ≤} (hf‚ÇÄ : f b = 1) :
    ‚àè' a : s, f a = ‚àè' a : (s \ {b} : Set Œ≤), f a :=
  tprod_setElem_eq_tprod_setElem_diff s {b} fun _ ha ‚Ü¶ ha ‚ñ∏ hf‚ÇÄ

@[to_additive]
theorem tprod_eq_tprod_of_ne_zero_bij {g : Œ≥ ‚Üí Œ±} (i : mulSupport g ‚Üí Œ≤) (hi : Injective i)
    (hf : mulSupport f ‚äÜ Set.range i) (hfg : ‚àÄ x, f (i x) = g x) : ‚àè' x, f x = ‚àè' y, g y := by
  rw [‚Üê tprod_subtype_mulSupport g, ‚Üê hi.tprod_eq hf]
  simp only [hfg]
#align tsum_eq_tsum_of_ne_zero_bij tsum_eq_tsum_of_ne_zero_bij

@[to_additive]
theorem Equiv.tprod_eq_tprod_of_mulSupport {f : Œ≤ ‚Üí Œ±} {g : Œ≥ ‚Üí Œ±} (e : mulSupport f ‚âÉ mulSupport g)
    (he : ‚àÄ x, g (e x) = f x) : ‚àè' x, f x = ‚àè' y, g y :=
  .symm <| tprod_eq_tprod_of_ne_zero_bij _ (Subtype.val_injective.comp e.injective) (by simp) he
#align equiv.tsum_eq_tsum_of_support Equiv.tsum_eq_tsum_of_support

@[to_additive]
theorem tprod_dite_right (P : Prop) [Decidable P] (x : Œ≤ ‚Üí ¬¨P ‚Üí Œ±) :
    ‚àè' b : Œ≤, (if h : P then (1 : Œ±) else x b h) = if h : P then (1 : Œ±) else ‚àè' b : Œ≤, x b h := by
  by_cases hP : P <;> simp [hP]
#align tsum_dite_right tsum_dite_right

@[to_additive]
theorem tprod_dite_left (P : Prop) [Decidable P] (x : Œ≤ ‚Üí P ‚Üí Œ±) :
    ‚àè' b : Œ≤, (if h : P then x b h else 1) = if h : P then ‚àè' b : Œ≤, x b h else 1 := by
  by_cases hP : P <;> simp [hP]
#align tsum_dite_left tsum_dite_left

@[to_additive (attr := simp)]
lemma tprod_extend_zero {Œ≥ : Type*} {g : Œ≥ ‚Üí Œ≤} (hg : Injective g) (f : Œ≥ ‚Üí Œ±) :
    ‚àè' y, extend g f 1 y = ‚àè' x, f x := by
  have : mulSupport (extend g f 1) ‚äÜ Set.range g := mulSupport_subset_iff'.2 <| extend_apply' _ _
  simp_rw [‚Üê hg.tprod_eq this, hg.extend_apply]

variable [T2Space Œ±]

@[to_additive]
theorem Function.Surjective.tprod_eq_tprod_of_hasProd_iff_hasProd {Œ±' : Type*} [CommMonoid Œ±']
    [TopologicalSpace Œ±'] {e : Œ±' ‚Üí Œ±} (hes : Function.Surjective e) (h1 : e 1 = 1) {f : Œ≤ ‚Üí Œ±}
    {g : Œ≥ ‚Üí Œ±'} (h : ‚àÄ {a}, HasProd f (e a) ‚Üî HasProd g a) : ‚àè' b, f b = e (‚àè' c, g c) :=
  by_cases (fun x ‚Ü¶ (h.mpr x.hasProd).tprod_eq) fun hg : ¬¨Prodable g ‚Ü¶ by
    have hf : ¬¨Prodable f := mt (hes.prodable_iff_of_hasProd_iff @h).1 hg
    simp [tprod_def, hf, hg, h1]
#align function.surjective.tsum_eq_tsum_of_has_sum_iff_has_sum Function.Surjective.tsum_eq_tsum_of_hasSum_iff_hasSum

@[to_additive]
theorem tprod_eq_tprod_of_hasProd_iff_hasProd {f : Œ≤ ‚Üí Œ±} {g : Œ≥ ‚Üí Œ±}
    (h : ‚àÄ {a}, HasProd f a ‚Üî HasProd g a) : ‚àè' b, f b = ‚àè' c, g c :=
  surjective_id.tprod_eq_tprod_of_hasProd_iff_hasProd rfl @h
#align tsum_eq_tsum_of_has_sum_iff_has_sum tsum_eq_tsum_of_hasSum_iff_hasSum

section ContinuousMul

variable [ContinuousMul Œ±]

@[to_additive]
theorem tprod_mul (hf : Prodable f) (hg : Prodable g) :
    ‚àè' b, (f b * g b) = (‚àè' b, f b) * ‚àè' b, g b :=
  (hf.hasProd.mul hg.hasProd).tprod_eq
#align tsum_add tsum_add

@[to_additive]
theorem tprod_prod {f : Œ≥ ‚Üí Œ≤ ‚Üí Œ±} {s : Finset Œ≥} (hf : ‚àÄ i ‚àà s, Prodable (f i)) :
    ‚àè' b, ‚àè i in s, f i b = ‚àè i in s, ‚àè' b, f i b :=
  (hasProd_prod fun i hi ‚Ü¶ (hf i hi).hasProd).tprod_eq
#align tsum_sum tsum_sum

@[to_additive "Version of `tsum_eq_add_tsum_ite` for `AddCommMonoid` rather than `AddCommGroup`.
Requires a different convergence assumption involving `Function.update`."]
theorem tprod_eq_mul_tprod_ite' [DecidableEq Œ≤] {f : Œ≤ ‚Üí Œ±} (b : Œ≤) (hf : Prodable (update f b 1)) :
    ‚àè' x, f x = f b * ‚àè' x, ite (x = b) 1 (f x) :=
  calc
    ‚àè' x, f x = ‚àè' x, (ite (x = b) (f x) 1 * update f b 1 x) :=
      tprod_congr fun n ‚Ü¶ by split_ifs with h <;> simp [update_apply, h]
    _ = (‚àè' x, ite (x = b) (f x) 1) * ‚àè' x, update f b 1 x :=
      tprod_mul ‚ü®ite (b = b) (f b) 1, hasProd_single b fun b hb ‚Ü¶ if_neg hb‚ü© hf
    _ = ite (b = b) (f b) 1 * ‚àè' x, update f b 1 x := by
      congr
      exact tprod_eq_single b fun b' hb' ‚Ü¶ if_neg hb'
    _ = f b * ‚àè' x, ite (x = b) 1 (f x) := by
      simp only [update, eq_self_iff_true, if_true, eq_rec_constant, dite_eq_ite]
#align tsum_eq_add_tsum_ite' tsum_eq_add_tsum_ite'

@[to_additive]
theorem tprod_mul_tprod_compl {s : Set Œ≤} (hs : Prodable (f ‚àò (‚Üë) : s ‚Üí Œ±))
    (hsc : Prodable (f ‚àò (‚Üë) : ‚Üës·∂ú ‚Üí Œ±)) : (‚àè' x : s, f x) * ‚àè' x : ‚Üës·∂ú, f x = ‚àè' x, f x :=
  (hs.hasProd.mul_compl hsc.hasProd).tprod_eq.symm
#align tsum_add_tsum_compl tsum_add_tsum_compl

@[to_additive]
theorem tprod_union_disjoint {s t : Set Œ≤} (hd : Disjoint s t) (hs : Prodable (f ‚àò (‚Üë) : s ‚Üí Œ±))
    (ht : Prodable (f ‚àò (‚Üë) : t ‚Üí Œ±)) : ‚àè' x : ‚Üë(s ‚à™ t), f x = (‚àè' x : s, f x) * ‚àè' x : t, f x :=
  (hs.hasProd.mul_disjoint hd ht.hasProd).tprod_eq
#align tsum_union_disjoint tsum_union_disjoint

@[to_additive]
theorem tprod_finset_bUnion_disjoint {Œπ} {s : Finset Œπ} {t : Œπ ‚Üí Set Œ≤}
    (hd : (s : Set Œπ).Pairwise (Disjoint on t)) (hf : ‚àÄ i ‚àà s, Prodable (f ‚àò (‚Üë) : t i ‚Üí Œ±)) :
    ‚àè' x : ‚ãÉ i ‚àà s, t i, f x = ‚àè i in s, ‚àè' x : t i, f x :=
  (hasProd_prod_disjoint _ hd fun i hi ‚Ü¶ (hf i hi).hasProd).tprod_eq
#align tsum_finset_bUnion_disjoint tsum_finset_bUnion_disjoint

end ContinuousMul

end tprod
