/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl
-/
import Mathlib.Topology.Algebra.InfiniteSum.Defs
import Mathlib.Data.Fintype.BigOperators
import Mathlib.Topology.Algebra.Monoid

/-!
# Lemmas on infinite sums in topological monoids

This file contains many simple lemmas on `tsum`, `HasSum` etc, which are placed here in order to
keep the basic file of definitions as short as possible.

Results requiring a group (rather than monoid) structure on the target should go in `Group.lean`.

-/

noncomputable section

open Filter Finset Function

open scoped BigOperators Topology

variable {Î± Î² Î³ Î´ : Type*}

section HasSum

variable [AddCommMonoid Î±] [TopologicalSpace Î±]

variable {f g : Î² â†’ Î±} {a b : Î±} {s : Finset Î²}

/-- Constant zero function has sum `0` -/
theorem hasSum_zero : HasSum (fun _ â†¦ 0 : Î² â†’ Î±) 0 := by simp [HasSum, tendsto_const_nhds]
#align has_sum_zero hasSum_zero

theorem hasSum_empty [IsEmpty Î²] : HasSum f 0 := by
  convert @hasSum_zero Î± Î² _ _
#align has_sum_empty hasSum_empty

theorem summable_zero : Summable (fun _ â†¦ 0 : Î² â†’ Î±) :=
  hasSum_zero.summable
#align summable_zero summable_zero

theorem summable_empty [IsEmpty Î²] : Summable f :=
  hasSum_empty.summable
#align summable_empty summable_empty

theorem summable_congr (hfg : âˆ€ b, f b = g b) : Summable f â†” Summable g :=
  iff_of_eq (congr_arg Summable <| funext hfg)
#align summable_congr summable_congr

theorem Summable.congr (hf : Summable f) (hfg : âˆ€ b, f b = g b) : Summable g :=
  (summable_congr hfg).mp hf
#align summable.congr Summable.congr

theorem HasSum.hasSum_of_sum_eq {g : Î³ â†’ Î±}
    (h_eq : âˆ€ u : Finset Î³, âˆƒ v : Finset Î², âˆ€ v', v âŠ† v' â†’
      âˆƒ u', u âŠ† u' âˆ§ âˆ‘ x in u', g x = âˆ‘ b in v', f b)
    (hf : HasSum g a) : HasSum f a :=
  le_trans (map_atTop_finset_sum_le_of_sum_eq h_eq) hf
#align has_sum.has_sum_of_sum_eq HasSum.hasSum_of_sum_eq

theorem hasSum_iff_hasSum {g : Î³ â†’ Î±}
    (hâ‚ : âˆ€ u : Finset Î³, âˆƒ v : Finset Î², âˆ€ v', v âŠ† v' â†’
      âˆƒ u', u âŠ† u' âˆ§ âˆ‘ x in u', g x = âˆ‘ b in v', f b)
    (hâ‚‚ : âˆ€ v : Finset Î², âˆƒ u : Finset Î³, âˆ€ u', u âŠ† u' â†’
      âˆƒ v', v âŠ† v' âˆ§ âˆ‘ b in v', f b = âˆ‘ x in u', g x) :
    HasSum f a â†” HasSum g a :=
  âŸ¨HasSum.hasSum_of_sum_eq hâ‚‚, HasSum.hasSum_of_sum_eq hâ‚âŸ©
#align has_sum_iff_has_sum hasSum_iff_hasSum

theorem Function.Injective.summable_iff {g : Î³ â†’ Î²} (hg : Injective g)
    (hf : âˆ€ x âˆ‰ Set.range g, f x = 0) : Summable (f âˆ˜ g) â†” Summable f :=
  exists_congr fun _ â†¦ hg.hasSum_iff hf
#align function.injective.summable_iff Function.Injective.summable_iff

@[simp] theorem hasSum_extend_zero {g : Î² â†’ Î³} (hg : Injective g) :
    HasSum (extend g f 0) a â†” HasSum f a := by
  rw [â† hg.hasSum_iff, extend_comp hg]
  exact extend_apply' _ _

@[simp] theorem summable_extend_zero {g : Î² â†’ Î³} (hg : Injective g) :
    Summable (extend g f 0) â†” Summable f :=
  exists_congr fun _ â†¦ hasSum_extend_zero hg

theorem hasSum_subtype_iff_indicator {s : Set Î²} :
    HasSum (f âˆ˜ (â†‘) : s â†’ Î±) a â†” HasSum (s.indicator f) a := by
  rw [â† Set.indicator_range_comp, Subtype.range_coe,
    hasSum_subtype_iff_of_support_subset Set.support_indicator_subset]
#align has_sum_subtype_iff_indicator hasSum_subtype_iff_indicator

theorem summable_subtype_iff_indicator {s : Set Î²} :
    Summable (f âˆ˜ (â†‘) : s â†’ Î±) â†” Summable (s.indicator f) :=
  exists_congr fun _ â†¦ hasSum_subtype_iff_indicator
#align summable_subtype_iff_indicator summable_subtype_iff_indicator

@[simp]
theorem hasSum_subtype_support : HasSum (f âˆ˜ (â†‘) : support f â†’ Î±) a â†” HasSum f a :=
  hasSum_subtype_iff_of_support_subset <| Set.Subset.refl _
#align has_sum_subtype_support hasSum_subtype_support

protected theorem Finset.summable (s : Finset Î²) (f : Î² â†’ Î±) :
    Summable (f âˆ˜ (â†‘) : (â†‘s : Set Î²) â†’ Î±) :=
  (s.hasSum f).summable
#align finset.summable Finset.summable

protected theorem Set.Finite.summable {s : Set Î²} (hs : s.Finite) (f : Î² â†’ Î±) :
    Summable (f âˆ˜ (â†‘) : s â†’ Î±) := by
  have := hs.toFinset.summable f
  rwa [hs.coe_toFinset] at this
#align set.finite.summable Set.Finite.summable

theorem summable_of_finite_support (h : (support f).Finite) : Summable f := by
  apply summable_of_ne_finset_zero (s := h.toFinset); simp

theorem hasSum_single {f : Î² â†’ Î±} (b : Î²) (hf : âˆ€ (b') (_ : b' â‰  b), f b' = 0) : HasSum f (f b) :=
  suffices HasSum f (âˆ‘ b' in {b}, f b') by simpa using this
  hasSum_sum_of_ne_finset_zero <| by simpa [hf]
#align has_sum_single hasSum_single

@[simp] lemma hasSum_unique [Unique Î²] (f : Î² â†’ Î±) : HasSum f (f default) :=
  hasSum_single default (fun _ hb â†¦ False.elim <| hb <| Unique.uniq ..)

@[simp] lemma hasSum_singleton (m : Î²) (f : Î² â†’ Î±) : HasSum (({m} : Set Î²).restrict f) (f m) :=
  hasSum_unique (Set.restrict {m} f)

theorem hasSum_ite_eq (b : Î²) [DecidablePred (Â· = b)] (a : Î±) :
    HasSum (fun b' â†¦ if b' = b then a else 0) a := by
  convert @hasSum_single _ _ _ _ (fun b' â†¦ if b' = b then a else 0) b (fun b' hb' â†¦ if_neg hb')
  exact (if_pos rfl).symm
#align has_sum_ite_eq hasSum_ite_eq

theorem Equiv.hasSum_iff (e : Î³ â‰ƒ Î²) : HasSum (f âˆ˜ e) a â†” HasSum f a :=
  e.injective.hasSum_iff <| by simp
#align equiv.has_sum_iff Equiv.hasSum_iff

theorem Function.Injective.hasSum_range_iff {g : Î³ â†’ Î²} (hg : Injective g) :
    HasSum (fun x : Set.range g â†¦ f x) a â†” HasSum (f âˆ˜ g) a :=
  (Equiv.ofInjective g hg).hasSum_iff.symm
#align function.injective.has_sum_range_iff Function.Injective.hasSum_range_iff

theorem Equiv.summable_iff (e : Î³ â‰ƒ Î²) : Summable (f âˆ˜ e) â†” Summable f :=
  exists_congr fun _ â†¦ e.hasSum_iff
#align equiv.summable_iff Equiv.summable_iff

theorem Equiv.hasSum_iff_of_support {g : Î³ â†’ Î±} (e : support f â‰ƒ support g)
    (he : âˆ€ x : support f, g (e x) = f x) : HasSum f a â†” HasSum g a := by
  have : (g âˆ˜ (â†‘)) âˆ˜ e = f âˆ˜ (â†‘) := funext he
  rw [â† hasSum_subtype_support, â† this, e.hasSum_iff, hasSum_subtype_support]
#align equiv.has_sum_iff_of_support Equiv.hasSum_iff_of_support

theorem hasSum_iff_hasSum_of_ne_zero_bij {g : Î³ â†’ Î±} (i : support g â†’ Î²)
    (hi : Injective i) (hf : support f âŠ† Set.range i)
    (hfg : âˆ€ x, f (i x) = g x) : HasSum f a â†” HasSum g a :=
  Iff.symm <|
    Equiv.hasSum_iff_of_support
      (Equiv.ofBijective (fun x â†¦ âŸ¨i x, fun hx â†¦ x.coe_prop <| hfg x â–¸ hxâŸ©)
        âŸ¨fun _ _ h â†¦ hi <| Subtype.ext_iff.1 h, fun y â†¦
          (hf y.coe_prop).imp fun _ hx â†¦ Subtype.ext hxâŸ©)
      hfg
#align has_sum_iff_has_sum_of_ne_zero_bij hasSum_iff_hasSum_of_ne_zero_bij

theorem Equiv.summable_iff_of_support {g : Î³ â†’ Î±} (e : support f â‰ƒ support g)
    (he : âˆ€ x : support f, g (e x) = f x) : Summable f â†” Summable g :=
  exists_congr fun _ â†¦ e.hasSum_iff_of_support he
#align equiv.summable_iff_of_support Equiv.summable_iff_of_support

protected theorem HasSum.map [AddCommMonoid Î³] [TopologicalSpace Î³] (hf : HasSum f a) {G}
    [FunLike G Î± Î³] [AddMonoidHomClass G Î± Î³] (g : G) (hg : Continuous g) :
    HasSum (g âˆ˜ f) (g a) :=
  have : (g âˆ˜ fun s : Finset Î² â†¦ âˆ‘ b in s, f b) = fun s : Finset Î² â†¦ âˆ‘ b in s, g (f b) :=
    funext <| map_sum g _
  show Tendsto (fun s : Finset Î² â†¦ âˆ‘ b in s, g (f b)) atTop (ğ“ (g a)) from
    this â–¸ (hg.tendsto a).comp hf
#align has_sum.map HasSum.map

protected theorem Summable.map [AddCommMonoid Î³] [TopologicalSpace Î³] (hf : Summable f) {G}
    [FunLike G Î± Î³] [AddMonoidHomClass G Î± Î³] (g : G) (hg : Continuous g) : Summable (g âˆ˜ f) :=
  (hf.hasSum.map g hg).summable
#align summable.map Summable.map

protected theorem Summable.map_iff_of_leftInverse [AddCommMonoid Î³] [TopologicalSpace Î³] {G G'}
    [FunLike G Î± Î³] [AddMonoidHomClass G Î± Î³] [FunLike G' Î³ Î±] [AddMonoidHomClass G' Î³ Î±]
    (g : G) (g' : G') (hg : Continuous g)
    (hg' : Continuous g') (hinv : Function.LeftInverse g' g) : Summable (g âˆ˜ f) â†” Summable f :=
  âŸ¨fun h â†¦ by
    have := h.map _ hg'
    rwa [â† Function.comp.assoc, hinv.id] at this, fun h â†¦ h.map _ hgâŸ©
#align summable.map_iff_of_left_inverse Summable.map_iff_of_leftInverse

/-- A special case of `Summable.map_iff_of_leftInverse` for convenience -/
protected theorem Summable.map_iff_of_equiv [AddCommMonoid Î³] [TopologicalSpace Î³] {G}
    [EquivLike G Î± Î³] [AddEquivClass G Î± Î³] (g : G) (hg : Continuous g)
    (hg' : Continuous (EquivLike.inv g : Î³ â†’ Î±)) : Summable (g âˆ˜ f) â†” Summable f :=
  Summable.map_iff_of_leftInverse g (g : Î± â‰ƒ+ Î³).symm hg hg' (EquivLike.left_inv g)
#align summable.map_iff_of_equiv Summable.map_iff_of_equiv

theorem Function.Surjective.summable_iff_of_hasSum_iff {Î±' : Type*} [AddCommMonoid Î±']
    [TopologicalSpace Î±'] {e : Î±' â†’ Î±} (hes : Function.Surjective e) {f : Î² â†’ Î±} {g : Î³ â†’ Î±'}
    (he : âˆ€ {a}, HasSum f (e a) â†” HasSum g a) : Summable f â†” Summable g :=
  hes.exists.trans <| exists_congr <| @he
#align function.surjective.summable_iff_of_has_sum_iff Function.Surjective.summable_iff_of_hasSum_iff

variable [ContinuousAdd Î±]

theorem HasSum.add (hf : HasSum f a) (hg : HasSum g b) : HasSum (fun b â†¦ f b + g b) (a + b) := by
  dsimp only [HasSum] at hf hg âŠ¢
  simp_rw [sum_add_distrib]
  exact hf.add hg
#align has_sum.add HasSum.add

theorem Summable.add (hf : Summable f) (hg : Summable g) : Summable fun b â†¦ f b + g b :=
  (hf.hasSum.add hg.hasSum).summable
#align summable.add Summable.add

theorem hasSum_sum {f : Î³ â†’ Î² â†’ Î±} {a : Î³ â†’ Î±} {s : Finset Î³} :
    (âˆ€ i âˆˆ s, HasSum (f i) (a i)) â†’ HasSum (fun b â†¦ âˆ‘ i in s, f i b) (âˆ‘ i in s, a i) := by
  classical
  exact Finset.induction_on s (by simp only [hasSum_zero, sum_empty, forall_true_iff]) <| by
    -- Porting note: with some help, `simp` used to be able to close the goal
    simp (config := { contextual := true }) only [mem_insert, forall_eq_or_imp, not_false_iff,
      sum_insert, and_imp]
    exact fun x s _ IH hx h â†¦ hx.add (IH h)
#align has_sum_sum hasSum_sum

theorem summable_sum {f : Î³ â†’ Î² â†’ Î±} {s : Finset Î³} (hf : âˆ€ i âˆˆ s, Summable (f i)) :
    Summable fun b â†¦ âˆ‘ i in s, f i b :=
  (hasSum_sum fun i hi â†¦ (hf i hi).hasSum).summable
#align summable_sum summable_sum

theorem HasSum.add_disjoint {s t : Set Î²} (hs : Disjoint s t) (ha : HasSum (f âˆ˜ (â†‘) : s â†’ Î±) a)
    (hb : HasSum (f âˆ˜ (â†‘) : t â†’ Î±) b) : HasSum (f âˆ˜ (â†‘) : (s âˆª t : Set Î²) â†’ Î±) (a + b) := by
  rw [hasSum_subtype_iff_indicator] at *
  rw [Set.indicator_union_of_disjoint hs]
  exact ha.add hb
#align has_sum.add_disjoint HasSum.add_disjoint

theorem hasSum_sum_disjoint {Î¹} (s : Finset Î¹) {t : Î¹ â†’ Set Î²} {a : Î¹ â†’ Î±}
    (hs : (s : Set Î¹).Pairwise (Disjoint on t)) (hf : âˆ€ i âˆˆ s, HasSum (f âˆ˜ (â†‘) : t i â†’ Î±) (a i)) :
    HasSum (f âˆ˜ (â†‘) : (â‹ƒ i âˆˆ s, t i) â†’ Î±) (âˆ‘ i in s, a i) := by
  simp_rw [hasSum_subtype_iff_indicator] at *
  rw [Finset.indicator_biUnion _ _ hs]
  exact hasSum_sum hf
#align has_sum_sum_disjoint hasSum_sum_disjoint

theorem HasSum.add_isCompl {s t : Set Î²} (hs : IsCompl s t) (ha : HasSum (f âˆ˜ (â†‘) : s â†’ Î±) a)
    (hb : HasSum (f âˆ˜ (â†‘) : t â†’ Î±) b) : HasSum f (a + b) := by
  simpa [â† hs.compl_eq] using
    (hasSum_subtype_iff_indicator.1 ha).add (hasSum_subtype_iff_indicator.1 hb)
#align has_sum.add_is_compl HasSum.add_isCompl

theorem HasSum.add_compl {s : Set Î²} (ha : HasSum (f âˆ˜ (â†‘) : s â†’ Î±) a)
    (hb : HasSum (f âˆ˜ (â†‘) : (sá¶œ : Set Î²) â†’ Î±) b) : HasSum f (a + b) :=
  ha.add_isCompl isCompl_compl hb
#align has_sum.add_compl HasSum.add_compl

theorem Summable.add_compl {s : Set Î²} (hs : Summable (f âˆ˜ (â†‘) : s â†’ Î±))
    (hsc : Summable (f âˆ˜ (â†‘) : (sá¶œ : Set Î²) â†’ Î±)) : Summable f :=
  (hs.hasSum.add_compl hsc.hasSum).summable
#align summable.add_compl Summable.add_compl

theorem HasSum.compl_add {s : Set Î²} (ha : HasSum (f âˆ˜ (â†‘) : (sá¶œ : Set Î²) â†’ Î±) a)
    (hb : HasSum (f âˆ˜ (â†‘) : s â†’ Î±) b) : HasSum f (a + b) :=
  ha.add_isCompl isCompl_compl.symm hb
#align has_sum.compl_add HasSum.compl_add

theorem Summable.compl_add {s : Set Î²} (hs : Summable (f âˆ˜ (â†‘) : (sá¶œ : Set Î²) â†’ Î±))
    (hsc : Summable (f âˆ˜ (â†‘) : s â†’ Î±)) : Summable f :=
  (hs.hasSum.compl_add hsc.hasSum).summable
#align summable.compl_add Summable.compl_add

/-- Version of `HasSum.update` for `AddCommMonoid` rather than `AddCommGroup`.
Rather than showing that `f.update` has a specific sum in terms of `HasSum`,
it gives a relationship between the sums of `f` and `f.update` given that both exist. -/
theorem HasSum.update' {Î± Î² : Type*} [TopologicalSpace Î±] [AddCommMonoid Î±] [T2Space Î±]
    [ContinuousAdd Î±] [DecidableEq Î²] {f : Î² â†’ Î±} {a a' : Î±} (hf : HasSum f a) (b : Î²) (x : Î±)
    (hf' : HasSum (update f b x) a') : a + x = a' + f b := by
  have : âˆ€ b', f b' + ite (b' = b) x 0 = update f b x b' + ite (b' = b) (f b) 0 := by
    intro b'
    split_ifs with hb'
    Â· simpa only [Function.update_apply, hb', eq_self_iff_true] using add_comm (f b) x
    Â· simp only [Function.update_apply, hb', if_false]
  have h := hf.add (hasSum_ite_eq b x)
  simp_rw [this] at h
  exact HasSum.unique h (hf'.add (hasSum_ite_eq b (f b)))
#align has_sum.update' HasSum.update'

/-- Version of `hasSum_ite_sub_hasSum` for `AddCommMonoid` rather than `AddCommGroup`.
Rather than showing that the `ite` expression has a specific sum in terms of `HasSum`,
it gives a relationship between the sums of `f` and `ite (n = b) 0 (f n)` given that both exist. -/
theorem eq_add_of_hasSum_ite {Î± Î² : Type*} [TopologicalSpace Î±] [AddCommMonoid Î±] [T2Space Î±]
    [ContinuousAdd Î±] [DecidableEq Î²] {f : Î² â†’ Î±} {a : Î±} (hf : HasSum f a) (b : Î²) (a' : Î±)
    (hf' : HasSum (fun n â†¦ ite (n = b) 0 (f n)) a') : a = a' + f b := by
  refine' (add_zero a).symm.trans (hf.update' b 0 _)
  convert hf'
  apply update_apply
#align eq_add_of_has_sum_ite eq_add_of_hasSum_ite

end HasSum

section tsum

variable [AddCommMonoid Î±] [TopologicalSpace Î±] {f g : Î² â†’ Î±} {a aâ‚ aâ‚‚ : Î±}

theorem tsum_congr_set_coe (f : Î² â†’ Î±) {s t : Set Î²} (h : s = t) :
    âˆ‘' x : s, f x = âˆ‘' x : t, f x := by rw [h]
#align tsum_congr_subtype tsum_congr_set_coe

theorem tsum_congr_subtype (f : Î² â†’ Î±) {P Q : Î² â†’ Prop} (h : âˆ€ x, P x â†” Q x):
    âˆ‘' x : {x // P x}, f x = âˆ‘' x : {x // Q x}, f x :=
  tsum_congr_set_coe f <| Set.ext h

theorem tsum_eq_finsum (hf : (support f).Finite) :
    âˆ‘' b, f b = âˆ‘á¶  b, f b := by simp [tsum_def, summable_of_finite_support hf, hf]

theorem tsum_eq_sum' {s : Finset Î²} (hf : support f âŠ† s) :
    âˆ‘' b, f b = âˆ‘ b in s, f b := by
  rw [tsum_eq_finsum (s.finite_toSet.subset hf), finsum_eq_sum_of_support_subset _ hf]

theorem tsum_eq_sum {s : Finset Î²} (hf : âˆ€ b âˆ‰ s, f b = 0) :
    âˆ‘' b, f b = âˆ‘ b in s, f b :=
  tsum_eq_sum' <| support_subset_iff'.2 hf
#align tsum_eq_sum tsum_eq_sum

@[simp]
theorem tsum_zero : âˆ‘' _ : Î², (0 : Î±) = 0 := by rw [tsum_eq_finsum] <;> simp
#align tsum_zero tsum_zero
#align tsum_zero' tsum_zero

@[simp]
theorem tsum_empty [IsEmpty Î²] : âˆ‘' b, f b = 0 := by
  rw [tsum_eq_sum (s := (âˆ… : Finset Î²))] <;> simp
#align tsum_empty tsum_empty

theorem tsum_congr {f g : Î² â†’ Î±}
    (hfg : âˆ€ b, f b = g b) : âˆ‘' b, f b = âˆ‘' b, g b :=
  congr_arg tsum (funext hfg)
#align tsum_congr tsum_congr

theorem tsum_fintype [Fintype Î²] (f : Î² â†’ Î±) : âˆ‘' b, f b = âˆ‘ b, f b := by
  apply tsum_eq_sum; simp
#align tsum_fintype tsum_fintype

theorem sum_eq_tsum_indicator (f : Î² â†’ Î±) (s : Finset Î²) :
    âˆ‘ x in s, f x = âˆ‘' x, Set.indicator (â†‘s) f x := by
  rw [tsum_eq_sum' (Set.support_indicator_subset), Finset.sum_indicator_subset _ Finset.Subset.rfl]
#align sum_eq_tsum_indicator sum_eq_tsum_indicator

theorem tsum_bool (f : Bool â†’ Î±) : âˆ‘' i : Bool, f i = f false + f true := by
  rw [tsum_fintype, Fintype.sum_bool, add_comm]
#align tsum_bool tsum_bool

theorem tsum_eq_single {f : Î² â†’ Î±} (b : Î²) (hf : âˆ€ b' â‰  b, f b' = 0) :
    âˆ‘' b, f b = f b := by
  rw [tsum_eq_sum (s := {b}), sum_singleton]
  exact fun b' hb' â†¦ hf b' (by simpa using hb')
#align tsum_eq_single tsum_eq_single

theorem tsum_tsum_eq_single (f : Î² â†’ Î³ â†’ Î±) (b : Î²) (c : Î³) (hfb : âˆ€ b' â‰  b, f b' c = 0)
    (hfc : âˆ€ b', âˆ€ c' â‰  c, f b' c' = 0) : âˆ‘' (b') (c'), f b' c' = f b c :=
  calc
    âˆ‘' (b') (c'), f b' c' = âˆ‘' b', f b' c := tsum_congr fun b' â†¦ tsum_eq_single _ (hfc b')
    _ = f b c := tsum_eq_single _ hfb
#align tsum_tsum_eq_single tsum_tsum_eq_single

@[simp]
theorem tsum_ite_eq (b : Î²) [DecidablePred (Â· = b)] (a : Î±) :
    âˆ‘' b', (if b' = b then a else 0) = a := by
  rw [tsum_eq_single b]
  Â· simp
  Â· intro b' hb'; simp [hb']
#align tsum_ite_eq tsum_ite_eq

-- Porting note: Added nolint simpNF, simpNF falsely claims that lhs does not simplify under simp
@[simp, nolint simpNF]
theorem Finset.tsum_subtype (s : Finset Î²) (f : Î² â†’ Î±) :
    âˆ‘' x : { x // x âˆˆ s }, f x = âˆ‘ x in s, f x := by
  rw [â† sum_attach]; exact tsum_fintype _
#align finset.tsum_subtype Finset.tsum_subtype

theorem Finset.tsum_subtype' (s : Finset Î²) (f : Î² â†’ Î±) :
    âˆ‘' x : (s : Set Î²), f x = âˆ‘ x in s, f x := by simp
#align finset.tsum_subtype' Finset.tsum_subtype'

-- Porting note: Added nolint simpNF, simpNF falsely claims that lhs does not simplify under simp
@[simp, nolint simpNF]
theorem tsum_singleton (b : Î²) (f : Î² â†’ Î±) : âˆ‘' x : ({b} : Set Î²), f x = f b := by
  rw [â† coe_singleton, Finset.tsum_subtype', sum_singleton]
#align tsum_singleton tsum_singleton

open Classical in
theorem Function.Injective.tsum_eq {g : Î³ â†’ Î²} (hg : Injective g) {f : Î² â†’ Î±}
    (hf : support f âŠ† Set.range g) : âˆ‘' c, f (g c) = âˆ‘' b, f b := by
  have : support f = g '' support (f âˆ˜ g) := by
    rw [support_comp_eq_preimage, Set.image_preimage_eq_iff.2 hf]
  rw [â† Function.comp_def]
  by_cases hf_fin : (support f).Finite
  Â· have hfg_fin : (support (f âˆ˜ g)).Finite := hf_fin.preimage (hg.injOn _)
    lift g to Î³ â†ª Î² using hg
    simp_rw [tsum_eq_sum' hf_fin.coe_toFinset.ge, tsum_eq_sum' hfg_fin.coe_toFinset.ge,
      comp_apply, â† Finset.sum_map]
    refine Finset.sum_congr (Finset.coe_injective ?_) fun _ _ â†¦ rfl
    simp [this]
  Â· have hf_fin' : Â¬ Set.Finite (support (f âˆ˜ g)) := by
      rwa [this, Set.finite_image_iff (hg.injOn _)] at hf_fin
    simp_rw [tsum_def, if_neg hf_fin, if_neg hf_fin', Summable,
      hg.hasSum_iff (support_subset_iff'.1 hf)]

theorem Equiv.tsum_eq (e : Î³ â‰ƒ Î²) (f : Î² â†’ Î±) : âˆ‘' c, f (e c) = âˆ‘' b, f b :=
  e.injective.tsum_eq <| by simp
#align equiv.tsum_eq Equiv.tsum_eq

/-! ### `tsum` on subsets - part 1 -/

theorem tsum_subtype_eq_of_support_subset {f : Î² â†’ Î±} {s : Set Î²} (hs : support f âŠ† s) :
    âˆ‘' x : s, f x = âˆ‘' x, f x :=
  Subtype.val_injective.tsum_eq <| by simpa
#align tsum_subtype_eq_of_support_subset tsum_subtype_eq_of_support_subset

theorem tsum_subtype_support (f : Î² â†’ Î±) : âˆ‘' x : support f, f x = âˆ‘' x, f x :=
  tsum_subtype_eq_of_support_subset Set.Subset.rfl

theorem tsum_subtype (s : Set Î²) (f : Î² â†’ Î±) : âˆ‘' x : s, f x = âˆ‘' x, s.indicator f x := by
  rw [â† tsum_subtype_eq_of_support_subset Set.support_indicator_subset, tsum_congr]
  simp
#align tsum_subtype tsum_subtype

-- Porting note: Added nolint simpNF, simpNF falsely claims that lhs does not simplify under simp
@[simp, nolint simpNF]
theorem tsum_univ (f : Î² â†’ Î±) : âˆ‘' x : (Set.univ : Set Î²), f x = âˆ‘' x, f x :=
  tsum_subtype_eq_of_support_subset <| Set.subset_univ _
#align tsum_univ tsum_univ

theorem tsum_image {g : Î³ â†’ Î²} (f : Î² â†’ Î±) {s : Set Î³} (hg : Set.InjOn g s) :
    âˆ‘' x : g '' s, f x = âˆ‘' x : s, f (g x) :=
  ((Equiv.Set.imageOfInjOn _ _ hg).tsum_eq fun x â†¦ f x).symm
#align tsum_image tsum_image

theorem tsum_range {g : Î³ â†’ Î²} (f : Î² â†’ Î±) (hg : Injective g) :
    âˆ‘' x : Set.range g, f x = âˆ‘' x, f (g x) := by
  rw [â† Set.image_univ, tsum_image f (hg.injOn _)]
  simp_rw [â† comp_apply (g := g), tsum_univ (f âˆ˜ g)]
#align tsum_range tsum_range

/-- If `f b = 0` for all `b âˆˆ t`, then the sum over `f a` with `a âˆˆ s` is the same as the
sum over `f a` with `a âˆˆ s âˆ– t`. -/
lemma tsum_setElem_eq_tsum_setElem_diff {f : Î² â†’ Î±} (s t : Set Î²)
    (hfâ‚€ : âˆ€ b âˆˆ t, f b = 0) :
    âˆ‘' a : s, f a = âˆ‘' a : (s \ t : Set Î²), f a :=
  .symm <| (Set.inclusion_injective (Set.diff_subset s t)).tsum_eq (f := f âˆ˜ (â†‘)) <|
    support_subset_iff'.2 fun b hb â†¦ hfâ‚€ b <| by simpa using hb

/-- If `f b = 0`, then the sum over `f a` with `a âˆˆ s` is the same as the sum over `f a` for
`a âˆˆ s âˆ– {b}`. -/
lemma tsum_eq_tsum_diff_singleton {f : Î² â†’ Î±} (s : Set Î²) {b : Î²} (hfâ‚€ : f b = 0) :
    âˆ‘' a : s, f a = âˆ‘' a : (s \ {b} : Set Î²), f a :=
  tsum_setElem_eq_tsum_setElem_diff s {b} fun _ ha â†¦ ha â–¸ hfâ‚€

theorem tsum_eq_tsum_of_ne_zero_bij {g : Î³ â†’ Î±} (i : support g â†’ Î²) (hi : Injective i)
    (hf : support f âŠ† Set.range i) (hfg : âˆ€ x, f (i x) = g x) : âˆ‘' x, f x = âˆ‘' y, g y := by
  rw [â† tsum_subtype_support g, â† hi.tsum_eq hf]
  simp only [hfg]
#align tsum_eq_tsum_of_ne_zero_bij tsum_eq_tsum_of_ne_zero_bij

theorem Equiv.tsum_eq_tsum_of_support {f : Î² â†’ Î±} {g : Î³ â†’ Î±} (e : support f â‰ƒ support g)
    (he : âˆ€ x, g (e x) = f x) : âˆ‘' x, f x = âˆ‘' y, g y :=
  .symm <| tsum_eq_tsum_of_ne_zero_bij _ (Subtype.val_injective.comp e.injective) (by simp) he
#align equiv.tsum_eq_tsum_of_support Equiv.tsum_eq_tsum_of_support

theorem tsum_dite_right (P : Prop) [Decidable P] (x : Î² â†’ Â¬P â†’ Î±) :
    âˆ‘' b : Î², (if h : P then (0 : Î±) else x b h) = if h : P then (0 : Î±) else âˆ‘' b : Î², x b h := by
  by_cases hP : P <;> simp [hP]
#align tsum_dite_right tsum_dite_right

theorem tsum_dite_left (P : Prop) [Decidable P] (x : Î² â†’ P â†’ Î±) :
    âˆ‘' b : Î², (if h : P then x b h else 0) = if h : P then âˆ‘' b : Î², x b h else 0 := by
  by_cases hP : P <;> simp [hP]
#align tsum_dite_left tsum_dite_left

@[simp]
lemma tsum_extend_zero {Î³ : Type*} {g : Î³ â†’ Î²} (hg : Injective g) (f : Î³ â†’ Î±) :
    âˆ‘' y, extend g f 0 y = âˆ‘' x, f x := by
  have : support (extend g f 0) âŠ† Set.range g := support_subset_iff'.2 <| extend_apply' _ _
  simp_rw [â† hg.tsum_eq this, hg.extend_apply]

variable [T2Space Î±]

theorem Function.Surjective.tsum_eq_tsum_of_hasSum_iff_hasSum {Î±' : Type*} [AddCommMonoid Î±']
    [TopologicalSpace Î±'] {e : Î±' â†’ Î±} (hes : Function.Surjective e) (h0 : e 0 = 0) {f : Î² â†’ Î±}
    {g : Î³ â†’ Î±'} (h : âˆ€ {a}, HasSum f (e a) â†” HasSum g a) : âˆ‘' b, f b = e (âˆ‘' c, g c) :=
  by_cases (fun x â†¦ (h.mpr x.hasSum).tsum_eq) fun hg : Â¬Summable g â†¦ by
    have hf : Â¬Summable f := mt (hes.summable_iff_of_hasSum_iff @h).1 hg
    simp [tsum_def, hf, hg, h0]
#align function.surjective.tsum_eq_tsum_of_has_sum_iff_has_sum Function.Surjective.tsum_eq_tsum_of_hasSum_iff_hasSum

theorem tsum_eq_tsum_of_hasSum_iff_hasSum {f : Î² â†’ Î±} {g : Î³ â†’ Î±}
    (h : âˆ€ {a}, HasSum f a â†” HasSum g a) : âˆ‘' b, f b = âˆ‘' c, g c :=
  surjective_id.tsum_eq_tsum_of_hasSum_iff_hasSum rfl @h
#align tsum_eq_tsum_of_has_sum_iff_has_sum tsum_eq_tsum_of_hasSum_iff_hasSum

section ContinuousAdd

variable [ContinuousAdd Î±]

theorem tsum_add (hf : Summable f) (hg : Summable g) :
    âˆ‘' b, (f b + g b) = âˆ‘' b, f b + âˆ‘' b, g b :=
  (hf.hasSum.add hg.hasSum).tsum_eq
#align tsum_add tsum_add

theorem tsum_sum {f : Î³ â†’ Î² â†’ Î±} {s : Finset Î³} (hf : âˆ€ i âˆˆ s, Summable (f i)) :
    âˆ‘' b, âˆ‘ i in s, f i b = âˆ‘ i in s, âˆ‘' b, f i b :=
  (hasSum_sum fun i hi â†¦ (hf i hi).hasSum).tsum_eq
#align tsum_sum tsum_sum

/-- Version of `tsum_eq_add_tsum_ite` for `AddCommMonoid` rather than `AddCommGroup`.
Requires a different convergence assumption involving `Function.update`. -/
theorem tsum_eq_add_tsum_ite' [DecidableEq Î²] {f : Î² â†’ Î±} (b : Î²) (hf : Summable (update f b 0)) :
    âˆ‘' x, f x = f b + âˆ‘' x, ite (x = b) 0 (f x) :=
  calc
    âˆ‘' x, f x = âˆ‘' x, (ite (x = b) (f x) 0 + update f b 0 x) :=
      tsum_congr fun n â†¦ by split_ifs with h <;> simp [update_apply, h]
    _ = âˆ‘' x, ite (x = b) (f x) 0 + âˆ‘' x, update f b 0 x :=
      tsum_add âŸ¨ite (b = b) (f b) 0, hasSum_single b fun b hb â†¦ if_neg hbâŸ© hf
    _ = ite (b = b) (f b) 0 + âˆ‘' x, update f b 0 x := by
      congr
      exact tsum_eq_single b fun b' hb' â†¦ if_neg hb'
    _ = f b + âˆ‘' x, ite (x = b) 0 (f x) := by
      simp only [update, eq_self_iff_true, if_true, eq_rec_constant, dite_eq_ite]
#align tsum_eq_add_tsum_ite' tsum_eq_add_tsum_ite'

theorem tsum_add_tsum_compl {s : Set Î²} (hs : Summable (f âˆ˜ (â†‘) : s â†’ Î±))
    (hsc : Summable (f âˆ˜ (â†‘) : â†‘sá¶œ â†’ Î±)) : âˆ‘' x : s, f x + âˆ‘' x : â†‘sá¶œ, f x = âˆ‘' x, f x :=
  (hs.hasSum.add_compl hsc.hasSum).tsum_eq.symm
#align tsum_add_tsum_compl tsum_add_tsum_compl

theorem tsum_union_disjoint {s t : Set Î²} (hd : Disjoint s t) (hs : Summable (f âˆ˜ (â†‘) : s â†’ Î±))
    (ht : Summable (f âˆ˜ (â†‘) : t â†’ Î±)) : âˆ‘' x : â†‘(s âˆª t), f x = âˆ‘' x : s, f x + âˆ‘' x : t, f x :=
  (hs.hasSum.add_disjoint hd ht.hasSum).tsum_eq
#align tsum_union_disjoint tsum_union_disjoint

theorem tsum_finset_bUnion_disjoint {Î¹} {s : Finset Î¹} {t : Î¹ â†’ Set Î²}
    (hd : (s : Set Î¹).Pairwise (Disjoint on t)) (hf : âˆ€ i âˆˆ s, Summable (f âˆ˜ (â†‘) : t i â†’ Î±)) :
    âˆ‘' x : â‹ƒ i âˆˆ s, t i, f x = âˆ‘ i in s, âˆ‘' x : t i, f x :=
  (hasSum_sum_disjoint _ hd fun i hi â†¦ (hf i hi).hasSum).tsum_eq
#align tsum_finset_bUnion_disjoint tsum_finset_bUnion_disjoint

end ContinuousAdd

end tsum
