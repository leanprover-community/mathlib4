/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl
-/
import Mathlib.Topology.Algebra.InfiniteSum.Defs
import Mathlib.Data.Fintype.BigOperators
import Mathlib.Topology.Algebra.Monoid

/-!
# Lemmas on infinite sums and products in topological monoids

This file contains many simple lemmas on `tsum`, `HasSum` etc, which are placed here in order to
keep the basic file of definitions as short as possible.

Results requiring a group (rather than monoid) structure on the target should go in `Group.lean`.

-/

noncomputable section

open Filter Finset Function

open scoped BigOperators Topology

variable {Î± Î² Î³ Î´ : Type*}

section HasProd

variable [CommMonoid Î±] [TopologicalSpace Î±]
variable {f g : Î² â†’ Î±} {a b : Î±} {s : Finset Î²}

/-- Constant one function has product `1` -/
@[to_additive "Constant zero function has sum `0`"]
theorem hasProd_one : HasProd (fun _ â†¦ 1 : Î² â†’ Î±) 1 := by simp [HasProd, tendsto_const_nhds]
#align has_sum_zero hasSum_zero

@[to_additive]
theorem hasProd_empty [IsEmpty Î²] : HasProd f 1 := by
  convert @hasProd_one Î± Î² _ _
#align has_sum_empty hasSum_empty

@[to_additive]
theorem multipliable_one : Multipliable (fun _ â†¦ 1 : Î² â†’ Î±) :=
  hasProd_one.multipliable
#align summable_zero summable_zero

@[to_additive]
theorem multipliable_empty [IsEmpty Î²] : Multipliable f :=
  hasProd_empty.multipliable
#align summable_empty summable_empty

@[to_additive]
theorem multipliable_congr (hfg : âˆ€ b, f b = g b) : Multipliable f â†” Multipliable g :=
  iff_of_eq (congr_arg Multipliable <| funext hfg)
#align summable_congr summable_congr

@[to_additive]
theorem Multipliable.congr (hf : Multipliable f) (hfg : âˆ€ b, f b = g b) : Multipliable g :=
  (multipliable_congr hfg).mp hf
#align summable.congr Summable.congr

@[to_additive]
theorem HasProd.hasProd_of_prod_eq {g : Î³ â†’ Î±}
    (h_eq : âˆ€ u : Finset Î³, âˆƒ v : Finset Î², âˆ€ v', v âŠ† v' â†’
      âˆƒ u', u âŠ† u' âˆ§ âˆ x in u', g x = âˆ b in v', f b)
    (hf : HasProd g a) : HasProd f a :=
  le_trans (map_atTop_finset_prod_le_of_prod_eq h_eq) hf
#align has_sum.has_sum_of_sum_eq HasSum.hasSum_of_sum_eq

@[to_additive]
theorem hasProd_iff_hasProd {g : Î³ â†’ Î±}
    (hâ‚ : âˆ€ u : Finset Î³, âˆƒ v : Finset Î², âˆ€ v', v âŠ† v' â†’
      âˆƒ u', u âŠ† u' âˆ§ âˆ x in u', g x = âˆ b in v', f b)
    (hâ‚‚ : âˆ€ v : Finset Î², âˆƒ u : Finset Î³, âˆ€ u', u âŠ† u' â†’
      âˆƒ v', v âŠ† v' âˆ§ âˆ b in v', f b = âˆ x in u', g x) :
    HasProd f a â†” HasProd g a :=
  âŸ¨HasProd.hasProd_of_prod_eq hâ‚‚, HasProd.hasProd_of_prod_eq hâ‚âŸ©
#align has_sum_iff_has_sum hasSum_iff_hasSum

@[to_additive]
theorem Function.Injective.multipliable_iff {g : Î³ â†’ Î²} (hg : Injective g)
    (hf : âˆ€ x âˆ‰ Set.range g, f x = 1) : Multipliable (f âˆ˜ g) â†” Multipliable f :=
  exists_congr fun _ â†¦ hg.hasProd_iff hf
#align function.injective.summable_iff Function.Injective.summable_iff

@[to_additive (attr := simp)] theorem hasProd_extend_one {g : Î² â†’ Î³} (hg : Injective g) :
    HasProd (extend g f 1) a â†” HasProd f a := by
  rw [â† hg.hasProd_iff, extend_comp hg]
  exact extend_apply' _ _

@[to_additive (attr := simp)] theorem multipliable_extend_one {g : Î² â†’ Î³} (hg : Injective g) :
    Multipliable (extend g f 1) â†” Multipliable f :=
  exists_congr fun _ â†¦ hasProd_extend_one hg

@[to_additive]
theorem hasProd_subtype_iff_mulIndicator {s : Set Î²} :
    HasProd (f âˆ˜ (â†‘) : s â†’ Î±) a â†” HasProd (s.mulIndicator f) a := by
  rw [â† Set.mulIndicator_range_comp, Subtype.range_coe,
    hasProd_subtype_iff_of_mulSupport_subset Set.mulSupport_mulIndicator_subset]
#align has_sum_subtype_iff_indicator hasSum_subtype_iff_indicator

@[to_additive]
theorem multipliable_subtype_iff_mulIndicator {s : Set Î²} :
    Multipliable (f âˆ˜ (â†‘) : s â†’ Î±) â†” Multipliable (s.mulIndicator f) :=
  exists_congr fun _ â†¦ hasProd_subtype_iff_mulIndicator
#align summable_subtype_iff_indicator summable_subtype_iff_indicator

@[to_additive (attr := simp)]
theorem hasProd_subtype_mulSupport : HasProd (f âˆ˜ (â†‘) : mulSupport f â†’ Î±) a â†” HasProd f a :=
  hasProd_subtype_iff_of_mulSupport_subset <| Set.Subset.refl _
#align has_sum_subtype_support hasSum_subtype_support

@[to_additive]
protected theorem Finset.multipliable (s : Finset Î²) (f : Î² â†’ Î±) :
    Multipliable (f âˆ˜ (â†‘) : (â†‘s : Set Î²) â†’ Î±) :=
  (s.hasProd f).multipliable
#align finset.summable Finset.summable

@[to_additive]
protected theorem Set.Finite.multipliable {s : Set Î²} (hs : s.Finite) (f : Î² â†’ Î±) :
    Multipliable (f âˆ˜ (â†‘) : s â†’ Î±) := by
  have := hs.toFinset.multipliable f
  rwa [hs.coe_toFinset] at this
#align set.finite.summable Set.Finite.summable

@[to_additive]
theorem multipliable_of_finite_mulSupport (h : (mulSupport f).Finite) : Multipliable f := by
  apply multipliable_of_ne_finset_one (s := h.toFinset); simp

@[to_additive]
theorem hasProd_single {f : Î² â†’ Î±} (b : Î²) (hf : âˆ€ (b') (_ : b' â‰  b), f b' = 1) : HasProd f (f b) :=
  suffices HasProd f (âˆ b' in {b}, f b') by simpa using this
  hasProd_prod_of_ne_finset_one <| by simpa [hf]
#align has_sum_single hasSum_single

@[to_additive (attr := simp)] lemma hasProd_unique [Unique Î²] (f : Î² â†’ Î±) : HasProd f (f default) :=
  hasProd_single default (fun _ hb â†¦ False.elim <| hb <| Unique.uniq ..)

@[to_additive (attr := simp)]
lemma hasProd_singleton (m : Î²) (f : Î² â†’ Î±) : HasProd (({m} : Set Î²).restrict f) (f m) :=
  hasProd_unique (Set.restrict {m} f)

@[to_additive]
theorem hasProd_ite_eq (b : Î²) [DecidablePred (Â· = b)] (a : Î±) :
    HasProd (fun b' â†¦ if b' = b then a else 1) a := by
  convert @hasProd_single _ _ _ _ (fun b' â†¦ if b' = b then a else 1) b (fun b' hb' â†¦ if_neg hb')
  exact (if_pos rfl).symm
#align has_sum_ite_eq hasSum_ite_eq

@[to_additive]
theorem Equiv.hasProd_iff (e : Î³ â‰ƒ Î²) : HasProd (f âˆ˜ e) a â†” HasProd f a :=
  e.injective.hasProd_iff <| by simp
#align equiv.has_sum_iff Equiv.hasSum_iff

@[to_additive]
theorem Function.Injective.hasProd_range_iff {g : Î³ â†’ Î²} (hg : Injective g) :
    HasProd (fun x : Set.range g â†¦ f x) a â†” HasProd (f âˆ˜ g) a :=
  (Equiv.ofInjective g hg).hasProd_iff.symm
#align function.injective.has_sum_range_iff Function.Injective.hasSum_range_iff

@[to_additive]
theorem Equiv.multipliable_iff (e : Î³ â‰ƒ Î²) : Multipliable (f âˆ˜ e) â†” Multipliable f :=
  exists_congr fun _ â†¦ e.hasProd_iff
#align equiv.summable_iff Equiv.summable_iff

@[to_additive]
theorem Equiv.hasProd_iff_of_mulSupport {g : Î³ â†’ Î±} (e : mulSupport f â‰ƒ mulSupport g)
    (he : âˆ€ x : mulSupport f, g (e x) = f x) : HasProd f a â†” HasProd g a := by
  have : (g âˆ˜ (â†‘)) âˆ˜ e = f âˆ˜ (â†‘) := funext he
  rw [â† hasProd_subtype_mulSupport, â† this, e.hasProd_iff, hasProd_subtype_mulSupport]
#align equiv.has_sum_iff_of_support Equiv.hasSum_iff_of_support

@[to_additive]
theorem hasProd_iff_hasProd_of_ne_one_bij {g : Î³ â†’ Î±} (i : mulSupport g â†’ Î²)
    (hi : Injective i) (hf : mulSupport f âŠ† Set.range i)
    (hfg : âˆ€ x, f (i x) = g x) : HasProd f a â†” HasProd g a :=
  Iff.symm <|
    Equiv.hasProd_iff_of_mulSupport
      (Equiv.ofBijective (fun x â†¦ âŸ¨i x, fun hx â†¦ x.coe_prop <| hfg x â–¸ hxâŸ©)
        âŸ¨fun _ _ h â†¦ hi <| Subtype.ext_iff.1 h, fun y â†¦
          (hf y.coe_prop).imp fun _ hx â†¦ Subtype.ext hxâŸ©)
      hfg
#align has_sum_iff_has_sum_of_ne_zero_bij hasSum_iff_hasSum_of_ne_zero_bij

@[to_additive]
theorem Equiv.multipliable_iff_of_mulSupport {g : Î³ â†’ Î±} (e : mulSupport f â‰ƒ mulSupport g)
    (he : âˆ€ x : mulSupport f, g (e x) = f x) : Multipliable f â†” Multipliable g :=
  exists_congr fun _ â†¦ e.hasProd_iff_of_mulSupport he
#align equiv.summable_iff_of_support Equiv.summable_iff_of_support

@[to_additive]
protected theorem HasProd.map [CommMonoid Î³] [TopologicalSpace Î³] (hf : HasProd f a) {G}
    [FunLike G Î± Î³] [MonoidHomClass G Î± Î³] (g : G) (hg : Continuous g) :
    HasProd (g âˆ˜ f) (g a) :=
  have : (g âˆ˜ fun s : Finset Î² â†¦ âˆ b in s, f b) = fun s : Finset Î² â†¦ âˆ b in s, g (f b) :=
    funext <| map_prod g _
  show Tendsto (fun s : Finset Î² â†¦ âˆ b in s, g (f b)) atTop (ğ“ (g a)) from
    this â–¸ (hg.tendsto a).comp hf
#align has_sum.map HasSum.map

@[to_additive]
protected theorem Multipliable.map [CommMonoid Î³] [TopologicalSpace Î³] (hf : Multipliable f) {G}
    [FunLike G Î± Î³] [MonoidHomClass G Î± Î³] (g : G) (hg : Continuous g) : Multipliable (g âˆ˜ f) :=
  (hf.hasProd.map g hg).multipliable
#align summable.map Summable.map

@[to_additive]
protected theorem Multipliable.map_iff_of_leftInverse [CommMonoid Î³] [TopologicalSpace Î³] {G G'}
    [FunLike G Î± Î³] [MonoidHomClass G Î± Î³] [FunLike G' Î³ Î±] [MonoidHomClass G' Î³ Î±]
    (g : G) (g' : G') (hg : Continuous g) (hg' : Continuous g') (hinv : Function.LeftInverse g' g) :
    Multipliable (g âˆ˜ f) â†” Multipliable f :=
  âŸ¨fun h â†¦ by
    have := h.map _ hg'
    rwa [â† Function.comp.assoc, hinv.id] at this, fun h â†¦ h.map _ hgâŸ©
#align summable.map_iff_of_left_inverse Summable.map_iff_of_leftInverse

/-- "A special case of `Multipliable.map_iff_of_leftInverse` for convenience" -/
@[to_additive "A special case of `Summable.map_iff_of_leftInverse` for convenience"]
protected theorem Multipliable.map_iff_of_equiv [CommMonoid Î³] [TopologicalSpace Î³] {G}
    [EquivLike G Î± Î³] [MulEquivClass G Î± Î³] (g : G) (hg : Continuous g)
    (hg' : Continuous (EquivLike.inv g : Î³ â†’ Î±)) : Multipliable (g âˆ˜ f) â†” Multipliable f :=
  Multipliable.map_iff_of_leftInverse g (g : Î± â‰ƒ* Î³).symm hg hg' (EquivLike.left_inv g)
#align summable.map_iff_of_equiv Summable.map_iff_of_equiv

@[to_additive]
theorem Function.Surjective.multipliable_iff_of_hasProd_iff {Î±' : Type*} [CommMonoid Î±']
    [TopologicalSpace Î±'] {e : Î±' â†’ Î±} (hes : Function.Surjective e) {f : Î² â†’ Î±} {g : Î³ â†’ Î±'}
    (he : âˆ€ {a}, HasProd f (e a) â†” HasProd g a) : Multipliable f â†” Multipliable g :=
  hes.exists.trans <| exists_congr <| @he
#align function.surjective.summable_iff_of_has_sum_iff Function.Surjective.summable_iff_of_hasSum_iff

variable [ContinuousMul Î±]

@[to_additive]
theorem HasProd.mul (hf : HasProd f a) (hg : HasProd g b) :
    HasProd (fun b â†¦ f b * g b) (a * b) := by
  dsimp only [HasProd] at hf hg âŠ¢
  simp_rw [prod_mul_distrib]
  exact hf.mul hg
#align has_sum.add HasSum.add

@[to_additive]
theorem Multipliable.mul (hf : Multipliable f) (hg : Multipliable g) :
    Multipliable fun b â†¦ f b * g b :=
  (hf.hasProd.mul hg.hasProd).multipliable
#align summable.add Summable.add

@[to_additive]
theorem hasProd_prod {f : Î³ â†’ Î² â†’ Î±} {a : Î³ â†’ Î±} {s : Finset Î³} :
    (âˆ€ i âˆˆ s, HasProd (f i) (a i)) â†’ HasProd (fun b â†¦ âˆ i in s, f i b) (âˆ i in s, a i) := by
  classical
  exact Finset.induction_on s (by simp only [hasProd_one, prod_empty, forall_true_iff]) <| by
    -- Porting note: with some help, `simp` used to be able to close the goal
    simp (config := { contextual := true }) only [mem_insert, forall_eq_or_imp, not_false_iff,
      prod_insert, and_imp]
    exact fun x s _ IH hx h â†¦ hx.mul (IH h)
#align has_sum_sum hasSum_sum

@[to_additive]
theorem multipliable_prod {f : Î³ â†’ Î² â†’ Î±} {s : Finset Î³} (hf : âˆ€ i âˆˆ s, Multipliable (f i)) :
    Multipliable fun b â†¦ âˆ i in s, f i b :=
  (hasProd_prod fun i hi â†¦ (hf i hi).hasProd).multipliable
#align summable_sum summable_sum

@[to_additive]
theorem HasProd.mul_disjoint {s t : Set Î²} (hs : Disjoint s t) (ha : HasProd (f âˆ˜ (â†‘) : s â†’ Î±) a)
    (hb : HasProd (f âˆ˜ (â†‘) : t â†’ Î±) b) : HasProd (f âˆ˜ (â†‘) : (s âˆª t : Set Î²) â†’ Î±) (a * b) := by
  rw [hasProd_subtype_iff_mulIndicator] at *
  rw [Set.mulIndicator_union_of_disjoint hs]
  exact ha.mul hb
#align has_sum.add_disjoint HasSum.add_disjoint

@[to_additive]
theorem hasProd_prod_disjoint {Î¹} (s : Finset Î¹) {t : Î¹ â†’ Set Î²} {a : Î¹ â†’ Î±}
    (hs : (s : Set Î¹).Pairwise (Disjoint on t)) (hf : âˆ€ i âˆˆ s, HasProd (f âˆ˜ (â†‘) : t i â†’ Î±) (a i)) :
    HasProd (f âˆ˜ (â†‘) : (â‹ƒ i âˆˆ s, t i) â†’ Î±) (âˆ i in s, a i) := by
  simp_rw [hasProd_subtype_iff_mulIndicator] at *
  rw [Finset.mulIndicator_biUnion _ _ hs]
  exact hasProd_prod hf
#align has_sum_sum_disjoint hasSum_sum_disjoint

@[to_additive]
theorem HasProd.mul_isCompl {s t : Set Î²} (hs : IsCompl s t) (ha : HasProd (f âˆ˜ (â†‘) : s â†’ Î±) a)
    (hb : HasProd (f âˆ˜ (â†‘) : t â†’ Î±) b) : HasProd f (a * b) := by
  simpa [â† hs.compl_eq] using
    (hasProd_subtype_iff_mulIndicator.1 ha).mul (hasProd_subtype_iff_mulIndicator.1 hb)
#align has_sum.add_is_compl HasSum.add_isCompl

@[to_additive]
theorem HasProd.mul_compl {s : Set Î²} (ha : HasProd (f âˆ˜ (â†‘) : s â†’ Î±) a)
    (hb : HasProd (f âˆ˜ (â†‘) : (sá¶œ : Set Î²) â†’ Î±) b) : HasProd f (a * b) :=
  ha.mul_isCompl isCompl_compl hb
#align has_sum.add_compl HasSum.add_compl

@[to_additive]
theorem Multipliable.mul_compl {s : Set Î²} (hs : Multipliable (f âˆ˜ (â†‘) : s â†’ Î±))
    (hsc : Multipliable (f âˆ˜ (â†‘) : (sá¶œ : Set Î²) â†’ Î±)) : Multipliable f :=
  (hs.hasProd.mul_compl hsc.hasProd).multipliable
#align summable.add_compl Summable.add_compl

@[to_additive]
theorem HasProd.compl_mul {s : Set Î²} (ha : HasProd (f âˆ˜ (â†‘) : (sá¶œ : Set Î²) â†’ Î±) a)
    (hb : HasProd (f âˆ˜ (â†‘) : s â†’ Î±) b) : HasProd f (a * b) :=
  ha.mul_isCompl isCompl_compl.symm hb
#align has_sum.compl_add HasSum.compl_add

@[to_additive]
theorem Multipliable.compl_add {s : Set Î²} (hs : Multipliable (f âˆ˜ (â†‘) : (sá¶œ : Set Î²) â†’ Î±))
    (hsc : Multipliable (f âˆ˜ (â†‘) : s â†’ Î±)) : Multipliable f :=
  (hs.hasProd.compl_mul hsc.hasProd).multipliable
#align summable.compl_add Summable.compl_add

/-- Version of `HasProd.update` for `CommMonoid` rather than `CommGroup`.
Rather than showing that `f.update` has a specific product in terms of `HasProd`,
it gives a relationship between the products of `f` and `f.update` given that both exist. -/
@[to_additive "Version of `HasSum.update` for `AddCommMonoid` rather than `AddCommGroup`.
Rather than showing that `f.update` has a specific sum in terms of `HasSum`,
it gives a relationship between the sums of `f` and `f.update` given that both exist."]
theorem HasProd.update' {Î± Î² : Type*} [TopologicalSpace Î±] [CommMonoid Î±] [T2Space Î±]
    [ContinuousMul Î±] [DecidableEq Î²] {f : Î² â†’ Î±} {a a' : Î±} (hf : HasProd f a) (b : Î²) (x : Î±)
    (hf' : HasProd (update f b x) a') : a * x = a' * f b := by
  have : âˆ€ b', f b' * ite (b' = b) x 1 = update f b x b' * ite (b' = b) (f b) 1 := by
    intro b'
    split_ifs with hb'
    Â· simpa only [Function.update_apply, hb', eq_self_iff_true] using mul_comm (f b) x
    Â· simp only [Function.update_apply, hb', if_false]
  have h := hf.mul (hasProd_ite_eq b x)
  simp_rw [this] at h
  exact HasProd.unique h (hf'.mul (hasProd_ite_eq b (f b)))
#align has_sum.update' HasSum.update'

/-- Version of `hasProd_ite_div_hasProd` for `CommMonoid` rather than `CommGroup`.
Rather than showing that the `ite` expression has a specific product in terms of `HasProd`, it gives
a relationship between the products of `f` and `ite (n = b) 0 (f n)` given that both exist. -/
@[to_additive "Version of `hasSum_ite_sub_hasSum` for `AddCommMonoid` rather than `AddCommGroup`.
Rather than showing that the `ite` expression has a specific sum in terms of `HasSum`,
it gives a relationship between the sums of `f` and `ite (n = b) 0 (f n)` given that both exist."]
theorem eq_mul_of_hasProd_ite {Î± Î² : Type*} [TopologicalSpace Î±] [CommMonoid Î±] [T2Space Î±]
    [ContinuousMul Î±] [DecidableEq Î²] {f : Î² â†’ Î±} {a : Î±} (hf : HasProd f a) (b : Î²) (a' : Î±)
    (hf' : HasProd (fun n â†¦ ite (n = b) 1 (f n)) a') : a = a' * f b := by
  refine' (mul_one a).symm.trans (hf.update' b 1 _)
  convert hf'
  apply update_apply
#align eq_add_of_has_sum_ite eq_add_of_hasSum_ite

end HasProd

section tprod

variable [CommMonoid Î±] [TopologicalSpace Î±] {f g : Î² â†’ Î±} {a aâ‚ aâ‚‚ : Î±}

@[to_additive]
theorem tprod_congr_set_coe (f : Î² â†’ Î±) {s t : Set Î²} (h : s = t) :
    âˆ' x : s, f x = âˆ' x : t, f x := by rw [h]
#align tsum_congr_subtype tsum_congr_set_coe

@[to_additive]
theorem tprod_congr_subtype (f : Î² â†’ Î±) {P Q : Î² â†’ Prop} (h : âˆ€ x, P x â†” Q x):
    âˆ' x : {x // P x}, f x = âˆ' x : {x // Q x}, f x :=
  tprod_congr_set_coe f <| Set.ext h

@[to_additive]
theorem tprod_eq_finprod (hf : (mulSupport f).Finite) :
    âˆ' b, f b = âˆá¶  b, f b := by simp [tprod_def, multipliable_of_finite_mulSupport hf, hf]

@[to_additive]
theorem tprod_eq_prod' {s : Finset Î²} (hf : mulSupport f âŠ† s) :
    âˆ' b, f b = âˆ b in s, f b := by
  rw [tprod_eq_finprod (s.finite_toSet.subset hf), finprod_eq_prod_of_mulSupport_subset _ hf]

@[to_additive]
theorem tprod_eq_prod {s : Finset Î²} (hf : âˆ€ b âˆ‰ s, f b = 1) :
    âˆ' b, f b = âˆ b in s, f b :=
  tprod_eq_prod' <| mulSupport_subset_iff'.2 hf
#align tsum_eq_sum tsum_eq_sum

@[to_additive (attr := simp)]
theorem tprod_one : âˆ' _ : Î², (1 : Î±) = 1 := by rw [tprod_eq_finprod] <;> simp
#align tsum_zero tsum_zero
#align tsum_zero' tsum_zero

@[to_additive (attr := simp)]
theorem tprod_empty [IsEmpty Î²] : âˆ' b, f b = 1 := by
  rw [tprod_eq_prod (s := (âˆ… : Finset Î²))] <;> simp
#align tsum_empty tsum_empty

@[to_additive]
theorem tprod_congr {f g : Î² â†’ Î±}
    (hfg : âˆ€ b, f b = g b) : âˆ' b, f b = âˆ' b, g b :=
  congr_arg tprod (funext hfg)
#align tsum_congr tsum_congr

@[to_additive]
theorem tprod_fintype [Fintype Î²] (f : Î² â†’ Î±) : âˆ' b, f b = âˆ b, f b := by
  apply tprod_eq_prod; simp
#align tsum_fintype tsum_fintype

@[to_additive]
theorem prod_eq_tprod_mulIndicator (f : Î² â†’ Î±) (s : Finset Î²) :
    âˆ x in s, f x = âˆ' x, Set.mulIndicator (â†‘s) f x := by
  rw [tprod_eq_prod' (Set.mulSupport_mulIndicator_subset),
      Finset.prod_mulIndicator_subset _ Finset.Subset.rfl]
#align sum_eq_tsum_indicator sum_eq_tsum_indicator

@[to_additive]
theorem tprod_bool (f : Bool â†’ Î±) : âˆ' i : Bool, f i = f false * f true := by
  rw [tprod_fintype, Fintype.prod_bool, mul_comm]
#align tsum_bool tsum_bool

@[to_additive]
theorem tprod_eq_mulSingle {f : Î² â†’ Î±} (b : Î²) (hf : âˆ€ b' â‰  b, f b' = 1) :
    âˆ' b, f b = f b := by
  rw [tprod_eq_prod (s := {b}), prod_singleton]
  exact fun b' hb' â†¦ hf b' (by simpa using hb')
#align tsum_eq_single tsum_eq_single

@[to_additive]
theorem tprod_tprod_eq_mulSingle (f : Î² â†’ Î³ â†’ Î±) (b : Î²) (c : Î³) (hfb : âˆ€ b' â‰  b, f b' c = 1)
    (hfc : âˆ€ b', âˆ€ c' â‰  c, f b' c' = 1) : âˆ' (b') (c'), f b' c' = f b c :=
  calc
    âˆ' (b') (c'), f b' c' = âˆ' b', f b' c := tprod_congr fun b' â†¦ tprod_eq_mulSingle _ (hfc b')
    _ = f b c := tprod_eq_mulSingle _ hfb
#align tsum_tsum_eq_single tsum_tsum_eq_single

@[to_additive (attr := simp)]
theorem tprod_ite_eq (b : Î²) [DecidablePred (Â· = b)] (a : Î±) :
    âˆ' b', (if b' = b then a else 1) = a := by
  rw [tprod_eq_mulSingle b]
  Â· simp
  Â· intro b' hb'; simp [hb']
#align tsum_ite_eq tsum_ite_eq

-- Porting note: Added nolint simpNF, simpNF falsely claims that lhs does not simplify under simp
@[to_additive (attr := simp, nolint simpNF)]
theorem Finset.tprod_subtype (s : Finset Î²) (f : Î² â†’ Î±) :
    âˆ' x : { x // x âˆˆ s }, f x = âˆ x in s, f x := by
  rw [â† prod_attach]; exact tprod_fintype _
#align finset.tsum_subtype Finset.tsum_subtype

@[to_additive]
theorem Finset.tprod_subtype' (s : Finset Î²) (f : Î² â†’ Î±) :
    âˆ' x : (s : Set Î²), f x = âˆ x in s, f x := by simp
#align finset.tsum_subtype' Finset.tsum_subtype'

-- Porting note: Added nolint simpNF, simpNF falsely claims that lhs does not simplify under simp
@[to_additive (attr := simp, nolint simpNF)]
theorem tprod_singleton (b : Î²) (f : Î² â†’ Î±) : âˆ' x : ({b} : Set Î²), f x = f b := by
  rw [â† coe_singleton, Finset.tprod_subtype', prod_singleton]
#align tsum_singleton tsum_singleton

open scoped Classical in
@[to_additive]
theorem Function.Injective.tprod_eq {g : Î³ â†’ Î²} (hg : Injective g) {f : Î² â†’ Î±}
    (hf : mulSupport f âŠ† Set.range g) : âˆ' c, f (g c) = âˆ' b, f b := by
  have : mulSupport f = g '' mulSupport (f âˆ˜ g) := by
    rw [mulSupport_comp_eq_preimage, Set.image_preimage_eq_iff.2 hf]
  rw [â† Function.comp_def]
  by_cases hf_fin : (mulSupport f).Finite
  Â· have hfg_fin : (mulSupport (f âˆ˜ g)).Finite := hf_fin.preimage (hg.injOn _)
    lift g to Î³ â†ª Î² using hg
    simp_rw [tprod_eq_prod' hf_fin.coe_toFinset.ge, tprod_eq_prod' hfg_fin.coe_toFinset.ge,
      comp_apply, â† Finset.prod_map]
    refine Finset.prod_congr (Finset.coe_injective ?_) fun _ _ â†¦ rfl
    simp [this]
  Â· have hf_fin' : Â¬ Set.Finite (mulSupport (f âˆ˜ g)) := by
      rwa [this, Set.finite_image_iff (hg.injOn _)] at hf_fin
    simp_rw [tprod_def, if_neg hf_fin, if_neg hf_fin', Multipliable,
      hg.hasProd_iff (mulSupport_subset_iff'.1 hf)]

@[to_additive]
theorem Equiv.tprod_eq (e : Î³ â‰ƒ Î²) (f : Î² â†’ Î±) : âˆ' c, f (e c) = âˆ' b, f b :=
  e.injective.tprod_eq <| by simp
#align equiv.tsum_eq Equiv.tsum_eq

/-! ### `tprod` on subsets - part 1 -/

@[to_additive]
theorem tprod_subtype_eq_of_mulSupport_subset {f : Î² â†’ Î±} {s : Set Î²} (hs : mulSupport f âŠ† s) :
    âˆ' x : s, f x = âˆ' x, f x :=
  Subtype.val_injective.tprod_eq <| by simpa
#align tsum_subtype_eq_of_support_subset tsum_subtype_eq_of_support_subset

@[to_additive]
theorem tprod_subtype_mulSupport (f : Î² â†’ Î±) : âˆ' x : mulSupport f, f x = âˆ' x, f x :=
  tprod_subtype_eq_of_mulSupport_subset Set.Subset.rfl

@[to_additive]
theorem tprod_subtype (s : Set Î²) (f : Î² â†’ Î±) : âˆ' x : s, f x = âˆ' x, s.mulIndicator f x := by
  rw [â† tprod_subtype_eq_of_mulSupport_subset Set.mulSupport_mulIndicator_subset, tprod_congr]
  simp
#align tsum_subtype tsum_subtype

-- Porting note: Added nolint simpNF, simpNF falsely claims that lhs does not simplify under simp
@[to_additive (attr := simp, nolint simpNF)]
theorem tprod_univ (f : Î² â†’ Î±) : âˆ' x : (Set.univ : Set Î²), f x = âˆ' x, f x :=
  tprod_subtype_eq_of_mulSupport_subset <| Set.subset_univ _
#align tsum_univ tsum_univ

@[to_additive]
theorem tprod_image {g : Î³ â†’ Î²} (f : Î² â†’ Î±) {s : Set Î³} (hg : Set.InjOn g s) :
    âˆ' x : g '' s, f x = âˆ' x : s, f (g x) :=
  ((Equiv.Set.imageOfInjOn _ _ hg).tprod_eq fun x â†¦ f x).symm
#align tsum_image tsum_image

@[to_additive]
theorem tprod_range {g : Î³ â†’ Î²} (f : Î² â†’ Î±) (hg : Injective g) :
    âˆ' x : Set.range g, f x = âˆ' x, f (g x) := by
  rw [â† Set.image_univ, tprod_image f (hg.injOn _)]
  simp_rw [â† comp_apply (g := g), tprod_univ (f âˆ˜ g)]
#align tsum_range tsum_range

/-- If `f b = 1` for all `b âˆˆ t`, then the product of `f a` with `a âˆˆ s` is the same as the
product of `f a` with `a âˆˆ s âˆ– t`. -/
@[to_additive "If `f b = 0` for all `b âˆˆ t`, then the sum of `f a` with `a âˆˆ s` is the same as the
sum of `f a` with `a âˆˆ s âˆ– t`."]
lemma tprod_setElem_eq_tprod_setElem_diff {f : Î² â†’ Î±} (s t : Set Î²)
    (hfâ‚€ : âˆ€ b âˆˆ t, f b = 1) :
    âˆ' a : s, f a = âˆ' a : (s \ t : Set Î²), f a :=
  .symm <| (Set.inclusion_injective (Set.diff_subset s t)).tprod_eq (f := f âˆ˜ (â†‘)) <|
    mulSupport_subset_iff'.2 fun b hb â†¦ hfâ‚€ b <| by simpa using hb

/-- If `f b = 1`, then the product of `f a` with `a âˆˆ s` is the same as the product of `f a` for
`a âˆˆ s âˆ– {b}`. -/
@[to_additive "If `f b = 0`, then the sum of `f a` with `a âˆˆ s` is the same as the sum of `f a`
for `a âˆˆ s âˆ– {b}`."]
lemma tprod_eq_tprod_diff_singleton {f : Î² â†’ Î±} (s : Set Î²) {b : Î²} (hfâ‚€ : f b = 1) :
    âˆ' a : s, f a = âˆ' a : (s \ {b} : Set Î²), f a :=
  tprod_setElem_eq_tprod_setElem_diff s {b} fun _ ha â†¦ ha â–¸ hfâ‚€

@[to_additive]
theorem tprod_eq_tprod_of_ne_one_bij {g : Î³ â†’ Î±} (i : mulSupport g â†’ Î²) (hi : Injective i)
    (hf : mulSupport f âŠ† Set.range i) (hfg : âˆ€ x, f (i x) = g x) : âˆ' x, f x = âˆ' y, g y := by
  rw [â† tprod_subtype_mulSupport g, â† hi.tprod_eq hf]
  simp only [hfg]
#align tsum_eq_tsum_of_ne_zero_bij tsum_eq_tsum_of_ne_zero_bij

@[to_additive]
theorem Equiv.tprod_eq_tprod_of_mulSupport {f : Î² â†’ Î±} {g : Î³ â†’ Î±}
    (e : mulSupport f â‰ƒ mulSupport g) (he : âˆ€ x, g (e x) = f x) :
    âˆ' x, f x = âˆ' y, g y :=
  .symm <| tprod_eq_tprod_of_ne_one_bij _ (Subtype.val_injective.comp e.injective) (by simp) he
#align equiv.tsum_eq_tsum_of_support Equiv.tsum_eq_tsum_of_support

@[to_additive]
theorem tprod_dite_right (P : Prop) [Decidable P] (x : Î² â†’ Â¬P â†’ Î±) :
    âˆ' b : Î², (if h : P then (1 : Î±) else x b h) = if h : P then (1 : Î±) else âˆ' b : Î², x b h := by
  by_cases hP : P <;> simp [hP]
#align tsum_dite_right tsum_dite_right

@[to_additive]
theorem tprod_dite_left (P : Prop) [Decidable P] (x : Î² â†’ P â†’ Î±) :
    âˆ' b : Î², (if h : P then x b h else 1) = if h : P then âˆ' b : Î², x b h else 1 := by
  by_cases hP : P <;> simp [hP]
#align tsum_dite_left tsum_dite_left

@[to_additive (attr := simp)]
lemma tprod_extend_one {Î³ : Type*} {g : Î³ â†’ Î²} (hg : Injective g) (f : Î³ â†’ Î±) :
    âˆ' y, extend g f 1 y = âˆ' x, f x := by
  have : mulSupport (extend g f 1) âŠ† Set.range g := mulSupport_subset_iff'.2 <| extend_apply' _ _
  simp_rw [â† hg.tprod_eq this, hg.extend_apply]

variable [T2Space Î±]

@[to_additive]
theorem Function.Surjective.tprod_eq_tprod_of_hasProd_iff_hasProd {Î±' : Type*} [CommMonoid Î±']
    [TopologicalSpace Î±'] {e : Î±' â†’ Î±} (hes : Function.Surjective e) (h1 : e 1 = 1) {f : Î² â†’ Î±}
    {g : Î³ â†’ Î±'} (h : âˆ€ {a}, HasProd f (e a) â†” HasProd g a) : âˆ' b, f b = e (âˆ' c, g c) :=
  by_cases (fun x â†¦ (h.mpr x.hasProd).tprod_eq) fun hg : Â¬Multipliable g â†¦ by
    have hf : Â¬Multipliable f := mt (hes.multipliable_iff_of_hasProd_iff @h).1 hg
    simp [tprod_def, hf, hg, h1]
#align function.surjective.tsum_eq_tsum_of_has_sum_iff_has_sum Function.Surjective.tsum_eq_tsum_of_hasSum_iff_hasSum

@[to_additive]
theorem tprod_eq_tprod_of_hasProd_iff_hasProd {f : Î² â†’ Î±} {g : Î³ â†’ Î±}
    (h : âˆ€ {a}, HasProd f a â†” HasProd g a) : âˆ' b, f b = âˆ' c, g c :=
  surjective_id.tprod_eq_tprod_of_hasProd_iff_hasProd rfl @h
#align tsum_eq_tsum_of_has_sum_iff_has_sum tsum_eq_tsum_of_hasSum_iff_hasSum

section ContinuousMul

variable [ContinuousMul Î±]

@[to_additive]
theorem tprod_mul (hf : Multipliable f) (hg : Multipliable g) :
    âˆ' b, (f b * g b) = (âˆ' b, f b) * âˆ' b, g b :=
  (hf.hasProd.mul hg.hasProd).tprod_eq
#align tsum_add tsum_add

@[to_additive tsum_sum]
theorem tprod_of_prod {f : Î³ â†’ Î² â†’ Î±} {s : Finset Î³} (hf : âˆ€ i âˆˆ s, Multipliable (f i)) :
    âˆ' b, âˆ i in s, f i b = âˆ i in s, âˆ' b, f i b :=
  (hasProd_prod fun i hi â†¦ (hf i hi).hasProd).tprod_eq
#align tsum_sum tsum_sum

/-- Version of `tprod_eq_mul_tprod_ite` for `CommMonoid` rather than `CommGroup`.
Requires a different convergence assumption involving `Function.update`. -/
@[to_additive "Version of `tsum_eq_add_tsum_ite` for `AddCommMonoid` rather than `AddCommGroup`.
Requires a different convergence assumption involving `Function.update`."]
theorem tprod_eq_mul_tprod_ite' [DecidableEq Î²] {f : Î² â†’ Î±} (b : Î²)
    (hf : Multipliable (update f b 1)) :
    âˆ' x, f x = f b * âˆ' x, ite (x = b) 1 (f x) :=
  calc
    âˆ' x, f x = âˆ' x, (ite (x = b) (f x) 1 * update f b 1 x) :=
      tprod_congr fun n â†¦ by split_ifs with h <;> simp [update_apply, h]
    _ = (âˆ' x, ite (x = b) (f x) 1) * âˆ' x, update f b 1 x :=
      tprod_mul âŸ¨ite (b = b) (f b) 1, hasProd_single b fun b hb â†¦ if_neg hbâŸ© hf
    _ = ite (b = b) (f b) 1 * âˆ' x, update f b 1 x := by
      congr
      exact tprod_eq_mulSingle b fun b' hb' â†¦ if_neg hb'
    _ = f b * âˆ' x, ite (x = b) 1 (f x) := by
      simp only [update, eq_self_iff_true, if_true, eq_rec_constant, dite_eq_ite]
#align tsum_eq_add_tsum_ite' tsum_eq_add_tsum_ite'

@[to_additive]
theorem tprod_mul_tprod_compl {s : Set Î²} (hs : Multipliable (f âˆ˜ (â†‘) : s â†’ Î±))
    (hsc : Multipliable (f âˆ˜ (â†‘) : â†‘sá¶œ â†’ Î±)) : (âˆ' x : s, f x) * âˆ' x : â†‘sá¶œ, f x = âˆ' x, f x :=
  (hs.hasProd.mul_compl hsc.hasProd).tprod_eq.symm
#align tsum_add_tsum_compl tsum_add_tsum_compl

@[to_additive]
theorem tprod_union_disjoint {s t : Set Î²} (hd : Disjoint s t) (hs : Multipliable (f âˆ˜ (â†‘) : s â†’ Î±))
    (ht : Multipliable (f âˆ˜ (â†‘) : t â†’ Î±)) :
    âˆ' x : â†‘(s âˆª t), f x = (âˆ' x : s, f x) * âˆ' x : t, f x :=
  (hs.hasProd.mul_disjoint hd ht.hasProd).tprod_eq
#align tsum_union_disjoint tsum_union_disjoint

@[to_additive]
theorem tprod_finset_bUnion_disjoint {Î¹} {s : Finset Î¹} {t : Î¹ â†’ Set Î²}
    (hd : (s : Set Î¹).Pairwise (Disjoint on t)) (hf : âˆ€ i âˆˆ s, Multipliable (f âˆ˜ (â†‘) : t i â†’ Î±)) :
    âˆ' x : â‹ƒ i âˆˆ s, t i, f x = âˆ i in s, âˆ' x : t i, f x :=
  (hasProd_prod_disjoint _ hd fun i hi â†¦ (hf i hi).hasProd).tprod_eq
#align tsum_finset_bUnion_disjoint tsum_finset_bUnion_disjoint

end ContinuousMul

end tprod
