/-
Copyright (c) 2024 Mitchell Lee. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mitchell Lee
-/
import Mathlib.Topology.Algebra.InfiniteSum.Group
import Mathlib.Topology.Algebra.Nonarchimedean.Basic

/-!
# Infinite sums and products in nonarchimedean abelian groups

Let `G` be a complete nonarchimedean abelian group and let `f : Œ± ‚Üí G` be a function. We prove that
`f` is unconditionally summable if and only if `f a` tends to zero on the cofinite filter on `Œ±`.
We also prove the analogous result in the multiplicative setting.

-/

open Filter Topology

namespace NonarchimedeanGroup

variable {Œ± G : Type*}
variable [CommGroup G] [UniformSpace G] [UniformGroup G] [NonarchimedeanGroup G]

/-- Let `G` be a nonarchimedean multiplicative abelian group, and let `f : Œ± ‚Üí G` be a function that
tends to one on the filter of cofinite sets. For each finite subset of `Œ±`, consider the partial
product of `f` on that subset. These partial products form a Cauchy filter. -/
@[to_additive "Let `G` be a nonarchimedean additive abelian group, and let `f : Œ± ‚Üí G` be a function
that tends to zero on the filter of cofinite sets. For each finite subset of `Œ±`, consider the
partial sum of `f` on that subset. These partial sums form a Cauchy filter."]
theorem cauchySeq_prod_of_tendsto_cofinite_one {f : Œ± ‚Üí G} (hf : Tendsto f cofinite (ùìù 1)) :
    CauchySeq (fun s ‚Ü¶ ‚àè i ‚àà s, f i) := by
  /- Let `U` be a neighborhood of `1`. It suffices to show that there exists `s : Finset Œ±` such
  that for any `t : Finset Œ±` disjoint from `s`, we have `‚àè i ‚àà t, f i ‚àà U`. -/
  apply cauchySeq_finset_iff_prod_vanishing.mpr
  intro U hU
  -- Since `G` is nonarchimedean, `U` contains an open subgroup `V`.
  rcases is_nonarchimedean U hU with ‚ü®V, hV‚ü©
  /- Let `s` be the set of all indices `i : Œ±` such that `f i ‚àâ V`. By our assumption `hf`, this is
  finite. -/
  use (tendsto_def.mp hf V V.mem_nhds_one).toFinset
  /- For any `t : Finset Œ±` disjoint from `s`, the product `‚àè i ‚àà t, f i` is a product of elements
  of `V`, so it is an element of `V` too. Thus, `‚àè i ‚àà t, f i ‚àà U`, as desired. -/
  intro t ht
  apply hV
  apply Subgroup.prod_mem
  intro i hi
  simpa using Finset.disjoint_left.mp ht hi

/-- Let `G` be a complete nonarchimedean multiplicative abelian group, and let `f : Œ± ‚Üí G` be a
function that tends to one on the filter of cofinite sets. Then `f` is unconditionally
multipliable. -/
@[to_additive "Let `G` be a complete nonarchimedean additive abelian group, and let `f : Œ± ‚Üí G` be a
function that tends to zero on the filter of cofinite sets. Then `f` is unconditionally summable."]
theorem multipliable_of_tendsto_cofinite_one [CompleteSpace G] {f : Œ± ‚Üí G}
    (hf : Tendsto f cofinite (ùìù 1)) : Multipliable f :=
  CompleteSpace.complete (cauchySeq_prod_of_tendsto_cofinite_one hf)

/-- Let `G` be a complete nonarchimedean multiplicative abelian group. Then a function `f : Œ± ‚Üí G`
is unconditionally multipliable if and only if it tends to one on the filter of cofinite sets. -/
@[to_additive "Let `G` be a complete nonarchimedean additive abelian group. Then a function
`f : Œ± ‚Üí G` is unconditionally summable if and only if it tends to zero on the filter of cofinite
sets."]
theorem multipliable_iff_tendsto_cofinite_one [CompleteSpace G] (f : Œ± ‚Üí G) :
    Multipliable f ‚Üî Tendsto f cofinite (ùìù 1) :=
  ‚ü®Multipliable.tendsto_cofinite_one, multipliable_of_tendsto_cofinite_one‚ü©

end NonarchimedeanGroup
