/-
Copyright (c) 2025 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/
import Mathlib.Analysis.Calculus.IteratedDeriv.Defs
import Mathlib.Analysis.Calculus.UniformLimitsDeriv
import Mathlib.Analysis.Normed.Group.FunctionSeries
import Mathlib.Topology.Algebra.InfiniteSum.UniformOn

/-!
# Differentiability of sum of functions

We prove some `HasSumUniformlyOn` versions of theorems from
`Mathlib.Analysis.Normed.Group.FunctionSeries`.

Alongside this we prove `derivWithin_tsum` which states that the derivative of a series of functions
is the sum of the derivatives, under suitable conditions we also prove an `iteratedDerivWithin`
version.

-/

open Set Metric TopologicalSpace Function Filter

open scoped Topology NNReal

variable {Î± Î² F : Type*} [NormedAddCommGroup F] [CompleteSpace F] {u : Î± â†’ â„}

theorem HasSumUniformlyOn.of_norm_le_summable {f : Î± â†’ Î² â†’ F} (hu : Summable u) {s : Set Î²}
    (hfu : âˆ€ n x, x âˆˆ s â†’ â€–f n xâ€– â‰¤ u n) : HasSumUniformlyOn f (fun x â†¦ âˆ‘' n, f n x) {s} :=  by
  simp [hasSumUniformlyOn_iff_tendstoUniformlyOn, tendstoUniformlyOn_tsum hu hfu]

theorem HasSumUniformlyOn.of_norm_le_summable_eventually {Î¹ : Type*} {f : Î¹ â†’ Î² â†’ F} {u : Î¹ â†’ â„}
    (hu : Summable u) {s : Set Î²} (hfu : âˆ€á¶  n in cofinite, âˆ€ x âˆˆ s, â€–f n xâ€– â‰¤ u n) :
    HasSumUniformlyOn f (fun x â†¦ âˆ‘' n, f n x) {s} := by
  simp [hasSumUniformlyOn_iff_tendstoUniformlyOn,
    tendstoUniformlyOn_tsum_of_cofinite_eventually hu hfu]

lemma SummableLocallyUniformlyOn.of_locally_bounded_eventually [TopologicalSpace Î²]
    [LocallyCompactSpace Î²] {f : Î± â†’ Î² â†’ F} {s : Set Î²} (hs : IsOpen s)
    (hu : âˆ€ K âŠ† s, IsCompact K â†’ âˆƒ u : Î± â†’ â„, Summable u âˆ§
    âˆ€á¶  n in cofinite, âˆ€ k âˆˆ K, â€–f n kâ€– â‰¤ u n) : SummableLocallyUniformlyOn f s := by
  apply HasSumLocallyUniformlyOn.summableLocallyUniformlyOn (g := fun x â†¦ âˆ‘' n, f n x)
  rw [hasSumLocallyUniformlyOn_iff_tendstoLocallyUniformlyOn,
    tendstoLocallyUniformlyOn_iff_forall_isCompact hs]
  intro K hK hKc
  obtain âŸ¨u, hu1, hu2âŸ© := hu K hK hKc
  exact tendstoUniformlyOn_tsum_of_cofinite_eventually hu1 hu2

lemma SummableLocallyUniformlyOn_of_locally_bounded [TopologicalSpace Î²] [LocallyCompactSpace Î²]
    {f : Î± â†’ Î² â†’ F} {s : Set Î²} (hs : IsOpen s)
    (hu : âˆ€ K âŠ† s, IsCompact K â†’ âˆƒ u : Î± â†’ â„, Summable u âˆ§ âˆ€ n, âˆ€ k âˆˆ K, â€–f n kâ€– â‰¤ u n) :
    SummableLocallyUniformlyOn f s := by
  apply SummableLocallyUniformlyOn.of_locally_bounded_eventually hs
  intro K hK hKc
  obtain âŸ¨u, hu1, hu2âŸ© := hu K hK hKc
  exact âŸ¨u, hu1, by filter_upwards using hu2âŸ©

/- The assumption T2Space can be dropped after the PR 31313 is merged. -/
variable {Î¹ : Type*} [AddCommMonoid Î±] {f : Î¹ â†’ Î² â†’ Î±} {s : Set Î²} [UniformSpace Î±]
  [ContinuousAdd Î±] [TopologicalSpace Î²] [T2Space Î±] {x : Î²}

section Continuous

/-- An infinite sum of continuous functions that converges uniformly on a set
is continuous. -/
theorem SummableUniformlyOn.continuousOn_tsum (hf : âˆ€ i, ContinuousOn (f i) s)
    (h : SummableUniformlyOn f {s}) : ContinuousOn (fun x => âˆ‘' n, f n x) s := by
  have := hasSumUniformlyOn_iff_tendstoUniformlyOn.mp h.hasSumUniformlyOn s (Set.mem_singleton s)
  refine this.continuousOn ?_
  filter_upwards with _
  fun_prop

/-- An infinite sum of continuous functions that converges uniformly is continuous. -/
theorem SummableUniformlyOn.continuous_tsum (hf : âˆ€ i, Continuous (f i))
    (h : SummableUniformlyOn f {univ}) : Continuous (fun x => âˆ‘' n, f n x) := by
  simp_all only [â† continuousOn_univ]
  exact SummableUniformlyOn.continuousOn_tsum hf h

/-- An infinite sum of continuous functions that converges locally uniformly on a set
is continuous. -/
theorem SummableLocallyUniformlyOn.continuousOn_tsum (hf : âˆ€ i, ContinuousOn (f i) s)
    (h : SummableLocallyUniformlyOn f s) : ContinuousOn (fun x => âˆ‘' n, f n x) s := by
  have := hasSumLocallyUniformlyOn_iff_tendstoLocallyUniformlyOn.mp h.hasSumLocallyUniformlyOn
  refine this.continuousOn ?_
  filter_upwards with _
  fun_prop

/-- An infinite sum of continuous functions that converges locally uniformly is continuous. -/
theorem SummableLocallyUniformlyOn.continuous_tsum (hf : âˆ€ i, Continuous (f i))
    (h : SummableLocallyUniformlyOn f univ) : Continuous (fun x => âˆ‘' n, f n x) := by
  simp_all only [â† continuousOn_univ]
  exact SummableLocallyUniformlyOn.continuousOn_tsum hf h

end Continuous

section Differentiable

variable {Î¹ F E : Type*} [NontriviallyNormedField E] [IsRCLikeNormedField E]
    [NormedAddCommGroup F] [NormedSpace E F] {s : Set E} {f : Î¹ â†’ E â†’ F} {x : E}

/-- If a sequence of functions `fâ‚™` is such that `âˆ‘ fâ‚™ (z)` is summable for each `z` in an
open set `s`, and `âˆ‘ (derivWithin fâ‚™ s) (z)` is summable uniformly on `s`, and each `fâ‚™` is
differentiable, then `âˆ‘ fâ‚™` is differentiable at each point in `s`. -/
theorem SummableUniformlyOn.hasDerivAt_tsum (hs : IsOpen s) (hx : x âˆˆ s)
    (hf : âˆ€ y âˆˆ s, Summable fun n â†¦ f n y)
    (h : SummableUniformlyOn (fun n â†¦ (derivWithin (f n) s)) {s})
    (hf2 : âˆ€ n r, r âˆˆ s â†’ DifferentiableAt E (f n) r) :
    HasDerivAt (fun z => âˆ‘' (n : Î¹), f n z) (âˆ‘' (n : Î¹), derivWithin (f n) s x) x := by
  apply hasDerivAt_of_tendstoUniformlyOn hs _ _ (fun y hy â†¦ (hf y hy).hasSum) hx
    (f' := fun n : Finset Î¹ â†¦ fun a â†¦ âˆ‘ i âˆˆ n, derivWithin (fun z â†¦ f i z) s a)
  Â· obtain âŸ¨g, hgâŸ© := h
    have : HasSumUniformlyOn (fun n â†¦ derivWithin (f n) s) g {s}:= hg
    apply (hasSumUniformlyOn_iff_tendstoUniformlyOn.mp hg s (Set.mem_singleton s)).congr_right
    exact fun _ hb â†¦ (this.tsum_eqOn (Set.mem_singleton s) hb).symm
  Â· filter_upwards with t r hr using HasDerivAt.fun_sum
      (fun q hq â†¦ ((hf2 q r hr).differentiableWithinAt.hasDerivWithinAt.hasDerivAt)
      (hs.mem_nhds hr))

/-- If a sequence of functions `fâ‚™` is such that `âˆ‘ fâ‚™ (z)` is summable for each `z` in an
open set `s`, and `âˆ‘ (derivWithin fâ‚™ s) (z)` is summable locally uniformly on `s`, and each `fâ‚™` is
differentiable, then `âˆ‘ fâ‚™` is differentiable at each point in `s`. -/
theorem SummableLocallyUniformlyOn.hasDerivAt_tsum (hs : IsOpen s) (hx : x âˆˆ s)
    (hf : âˆ€ y âˆˆ s, Summable fun n â†¦ f n y)
    (h : SummableLocallyUniformlyOn (fun n â†¦ (derivWithin (f n) s)) s)
    (hf2 : âˆ€ n r, r âˆˆ s â†’ DifferentiableAt E (f n) r) :
    HasDerivAt (fun z => âˆ‘' (n : Î¹), f n z) (âˆ‘' (n : Î¹), derivWithin (f n) s x) x := by
  apply hasDerivAt_of_tendstoLocallyUniformlyOn hs _ _ (fun y hy â†¦ (hf y hy).hasSum) hx
    (f' := fun n : Finset Î¹ â†¦ fun a â†¦ âˆ‘ i âˆˆ n, derivWithin (fun z â†¦ f i z) s a)
  Â· obtain âŸ¨g, hgâŸ© := h
    apply (hasSumLocallyUniformlyOn_iff_tendstoLocallyUniformlyOn.mp hg).congr_right
    exact fun _ hb â†¦ (hg.tsum_eqOn hb).symm
  Â· filter_upwards with t r hr using HasDerivAt.fun_sum
      (fun q hq â†¦ ((hf2 q r hr).differentiableWithinAt.hasDerivWithinAt.hasDerivAt)
      (hs.mem_nhds hr))

/-- If a sequence of functions `fâ‚™` is such that `âˆ‘ fâ‚™ (z)` is summable for each `z` in an
open set `s`, and `âˆ‘ (derivWithin fâ‚™ s) (z)` is summable uniformly on `s`, and each `fâ‚™` is
differentiable, then `âˆ‘ fâ‚™` is differentiable on `s`. -/
theorem SummableUniformlyOn.differentiableOn_tsum (hs : IsOpen s)
    (hf : âˆ€ y âˆˆ s, Summable fun n â†¦ f n y)
    (h : SummableUniformlyOn (fun n â†¦ (derivWithin (f n) s)) {s})
    (hf2 : âˆ€ n r, r âˆˆ s â†’ DifferentiableAt E (f n) r) :
    DifferentiableOn E (fun z => âˆ‘' (n : Î¹), f n z) s := by
  intro x hx
  apply HasDerivWithinAt.differentiableWithinAt (f' := âˆ‘' (n : Î¹), derivWithin (f n) s x)
  exact HasDerivAt.hasDerivWithinAt (SummableUniformlyOn.hasDerivAt_tsum hs hx hf h hf2)

/-- If a sequence of functions `fâ‚™` is such that `âˆ‘ fâ‚™ (z)` is summable for each `z` in an
open set `s`, and `âˆ‘ (derivWithin fâ‚™ s) (z)` is summable locally uniformly on `s`, and each `fâ‚™` is
differentiable, then `âˆ‘ fâ‚™` is differentiable on `s`. -/
theorem SummableLocallyUniformlyOn.differentiableOn_tsum (hs : IsOpen s)
    (hf : âˆ€ y âˆˆ s, Summable fun n â†¦ f n y)
    (h : SummableLocallyUniformlyOn (fun n â†¦ (derivWithin (f n) s)) s)
    (hf2 : âˆ€ n r, r âˆˆ s â†’ DifferentiableAt E (f n) r) :
    DifferentiableOn E (fun z => âˆ‘' (n : Î¹), f n z) s := by
  intro x hx
  apply HasDerivWithinAt.differentiableWithinAt (f' := âˆ‘' (n : Î¹), derivWithin (f n) s x)
  exact HasDerivAt.hasDerivWithinAt (SummableLocallyUniformlyOn.hasDerivAt_tsum hs hx hf h hf2)

/-- The `derivWithin` of a sum whose derivative is absolutely and uniformly convergent sum on an
open set `s` is the sum of the derivatives of sequence of functions on the open set `s` -/
theorem SummableUniformlyOn.derivWithin_tsum (hs : IsOpen s) (hx : x âˆˆ s)
    (hf : âˆ€ y âˆˆ s, Summable fun n â†¦ f n y)
    (h : SummableUniformlyOn (fun n â†¦ (derivWithin (f n) s)) {s})
    (hf2 : âˆ€ n r, r âˆˆ s â†’ DifferentiableAt E (f n) r) :
    derivWithin (fun z â†¦ âˆ‘' n, f n z) s x = âˆ‘' n, derivWithin (f n) s x := by
  apply HasDerivWithinAt.derivWithin ?_ (hs.uniqueDiffWithinAt hx)
  exact HasDerivAt.hasDerivWithinAt (SummableUniformlyOn.hasDerivAt_tsum hs hx hf h hf2)

/-- The `derivWithin` of a sum whose derivative is absolutely and uniformly convergent sum on an
open set `s` is the sum of the derivatives of sequence of functions on the open set `s` -/
theorem SummableLocallyUniformlyOn.derivWithin_tsum (hs : IsOpen s) (hx : x âˆˆ s)
    (hf : âˆ€ y âˆˆ s, Summable fun n â†¦ f n y)
    (h : SummableLocallyUniformlyOn (fun n â†¦ (derivWithin (f n) s)) s)
    (hf2 : âˆ€ n r, r âˆˆ s â†’ DifferentiableAt E (f n) r) :
    derivWithin (fun z â†¦ âˆ‘' n, f n z) s x = âˆ‘' n, derivWithin (f n) s x := by
  apply HasDerivWithinAt.derivWithin ?_ (hs.uniqueDiffWithinAt hx)
  exact HasDerivAt.hasDerivWithinAt (SummableLocallyUniformlyOn.hasDerivAt_tsum hs hx hf h hf2)

/-- If a sequence of functions `fâ‚™` is such that `âˆ‘ fâ‚™ (z)` is summable for each `z` in an
open set `s`, and for each `1 â‰¤ k â‰¤ m`, the series of `k`-th iterated derivatives
`âˆ‘ (iteratedDerivWithin k fâ‚™ s) (z)` is summable uniformly on `s`, and each `fâ‚™` is
`m`-times differentiable, then the `m`-th iterated derivative of the sum is the sum of the
`m`-th iterated derivatives. -/
theorem SummableUniformlyOn.iteratedDerivWithin_tsum (m : â„•) (hs : IsOpen s) (hx : x âˆˆ s)
    (hsum : âˆ€ t âˆˆ s, Summable (fun n : Î¹ â†¦ f n t))
    (h : âˆ€ k, 1 â‰¤ k â†’ k â‰¤ m â†’ SummableUniformlyOn (fun n â†¦ (iteratedDerivWithin k (f n) s)) {s})
    (hf2 : âˆ€ n k r, k < m â†’ r âˆˆ s â†’ DifferentiableAt E (iteratedDerivWithin k (f n) s) r) :
    iteratedDerivWithin m (fun z â†¦ âˆ‘' n, f n z) s x = âˆ‘' n, iteratedDerivWithin m (f n) s x := by
  induction m generalizing x with
  | zero => simp
  | succ m hm =>
    simp_rw [iteratedDerivWithin_succ]
    rw [â† SummableUniformlyOn.derivWithin_tsum hs hx _  _
      (fun n r hr â†¦ hf2 n m r (by cutsat) hr)]
    Â· exact derivWithin_congr (fun t ht â†¦ hm ht (fun k hk1 hkm â†¦ h k hk1 (by cutsat))
          (fun k r e hr he â†¦ hf2 k r e (by cutsat) he)) (hm hx (fun k hk1 hkm â†¦ h k hk1 (by cutsat))
          (fun k r e hr he â†¦ hf2 k r e (by cutsat) he))
    Â· intro r hr
      by_cases hm2 : m = 0
      Â· simp [hm2, hsum r hr]
      Â· exact ((h m (by cutsat) (by cutsat)).summable (Set.mem_singleton s) hr).congr
          (fun _ â†¦ by simp)
    Â· exact SummableUniformlyOn.congr
        (fun _ _ _ _ ht â†¦ iteratedDerivWithin_succ) (h (m + 1) (by cutsat) (by cutsat))

/-- If a sequence of functions `fâ‚™` is such that `âˆ‘ fâ‚™ (z)` is summable for each `z` in an
open set `s`, and for each `1 â‰¤ k â‰¤ m`, the series of `k`-th iterated derivatives
`âˆ‘ (iteratedDerivWithin k fâ‚™ s) (z)` is summable locally uniformly on `s`, and each `fâ‚™` is
`m`-times differentiable, then the `m`-th iterated derivative of the sum is the sum of the
`m`-th iterated derivatives. -/
theorem SummableLocallyUniformlyOn.iteratedDerivWithin_tsum (m : â„•) (hs : IsOpen s) (hx : x âˆˆ s)
    (hsum : âˆ€ t âˆˆ s, Summable (fun n : Î¹ â†¦ f n t))
    (h : âˆ€ k, 1 â‰¤ k â†’ k â‰¤ m â†’ SummableLocallyUniformlyOn
      (fun n â†¦ (iteratedDerivWithin k (f n) s)) s)
    (hf2 : âˆ€ n k r, k < m â†’ r âˆˆ s â†’ DifferentiableAt E (iteratedDerivWithin k (f n) s) r) :
    iteratedDerivWithin m (fun z â†¦ âˆ‘' n, f n z) s x = âˆ‘' n, iteratedDerivWithin m (f n) s x := by
  induction m generalizing x with
  | zero => simp
  | succ m hm =>
    simp_rw [iteratedDerivWithin_succ]
    rw [â† SummableLocallyUniformlyOn.derivWithin_tsum hs hx _  _
      (fun n r hr â†¦ hf2 n m r (by cutsat) hr)]
    Â· exact derivWithin_congr (fun t ht â†¦ hm ht (fun k hk1 hkm â†¦ h k hk1 (by cutsat))
          (fun k r e hr he â†¦ hf2 k r e (by cutsat) he)) (hm hx (fun k hk1 hkm â†¦ h k hk1 (by cutsat))
          (fun k r e hr he â†¦ hf2 k r e (by cutsat) he))
    Â· intro r hr
      by_cases hm2 : m = 0
      Â· simp [hm2, hsum r hr]
      Â· exact ((h m (by cutsat) (by cutsat)).summable hr).congr (fun _ â†¦ by simp)
    Â· exact SummableLocallyUniformlyOn_congr
        (fun _ _ ht â†¦ iteratedDerivWithin_succ) (h (m + 1) (by cutsat) (by cutsat))

/-- If a sequence of functions `fâ‚™` is such that for each `0 â‰¤ k â‰¤ N`, the series of `k`-th
iterated derivatives `âˆ‘ (iteratedDerivWithin k fâ‚™ s) (z)` is summable uniformly on `s`, and each
`fâ‚™` is in the class of `C^N`, then the series is also in `C^N`. -/
theorem SummableUniformlyOn.contDiffOn_tsum {N : â„•âˆ} (hs : IsOpen s)
    (hf : âˆ€ (n : Î¹), ContDiffOn E N (f n) s)
    (h : âˆ€ (k : â„•), k â‰¤ N â†’ SummableUniformlyOn (fun n â†¦ (iteratedDerivWithin k (f n) s)) {s}) :
    ContDiffOn E N (fun (x : E) => âˆ‘' (n : Î¹), f n x) s := by
  simp_all only [contDiffOn_iff_continuousOn_differentiableOn_deriv hs.uniqueDiffOn]
  have q (r : E) (hr : r âˆˆ s) : s âˆˆ ğ“ r := by exact IsOpen.mem_nhds hs hr
  have hsum : âˆ€ t âˆˆ s, Summable fun (n : Î¹) => f n t := fun t ht =>
    by simpa using SummableUniformlyOn.summable (h 0 (zero_le N)) (Set.mem_singleton s) ht
  constructor
  Â· intro m hm
    refine ContinuousOn.congr (f := fun x => âˆ‘' n, iteratedDerivWithin m (f n) s x) ?_ ?_
    Â· exact SummableUniformlyOn.continuousOn_tsum (fun i => (hf i).1 m hm) (h m hm)
    Â· intro x hx
      refine SummableUniformlyOn.iteratedDerivWithin_tsum m hs hx hsum ?_ ?_
      Â· intro k _ hk
        have : k â‰¤ N := LE.le.trans (mod_cast hk) hm
        exact h k this
      Â· intro n k r hk hr
        have p : k < N := lt_of_lt_of_le (mod_cast hk) hm
        exact ((hf n).2 k p).differentiableAt (q r hr)
  Â· intro m hm
    have h'm : ((m + 1 : â„•) : â„•âˆ) â‰¤ N := by
      simpa only [ENat.coe_add, ENat.coe_one] using Order.add_one_le_of_lt hm
    refine DifferentiableOn.congr (f := fun x => âˆ‘' n, iteratedDerivWithin m (f n) s x) ?_ ?_
    Â· apply SummableUniformlyOn.differentiableOn_tsum hs ?_ ?_ ?_
      Â· intro y hy
        exact SummableUniformlyOn.summable (h m hm.le) (Set.mem_singleton s) hy
      Â· apply SummableUniformlyOn.congr (f := fun n => iteratedDerivWithin (m + 1) (f n) s)
        Â· intro a ha i x hx; rw [â† iteratedDerivWithin_succ]
        Â· exact h (m + 1) h'm
      Â· intro n r hr
        exact ((hf n).2 m hm).differentiableAt (q r hr)
    Â· intro x hx
      rw [SummableUniformlyOn.iteratedDerivWithin_tsum m hs hx hsum]
      Â· intro k _ hk
        have : k â‰¤ N := LE.le.trans (mod_cast hk) hm.le
        exact h k this
      Â· intro n k r hk hr
        have p : k < N := LT.lt.trans (mod_cast hk) hm
        exact ((hf n).2 k p).differentiableAt (q r hr)

/-- If a sequence of functions `fâ‚™` is such that for each `0 â‰¤ k â‰¤ N`, the series of `k`-th
iterated derivatives `âˆ‘ (iteratedDerivWithin k fâ‚™ s) (z)` is summable locally uniformly on `s`, and
each `fâ‚™` is in the class of `C^N`, then the series is also in `C^N`. -/
theorem SummableLocallyUniformlyOn.contDiffOn_tsum {N : â„•âˆ} (hs : IsOpen s)
    (hf : âˆ€ (n : Î¹), ContDiffOn E N (f n) s)
    (h : âˆ€ (k : â„•), k â‰¤ N â†’ SummableLocallyUniformlyOn
      (fun n â†¦ (iteratedDerivWithin k (f n) s)) s) :
    ContDiffOn E N (fun (x : E) => âˆ‘' (n : Î¹), f n x) s := by
  simp_all only [contDiffOn_iff_continuousOn_differentiableOn_deriv hs.uniqueDiffOn]
  have q (r : E) (hr : r âˆˆ s) : s âˆˆ ğ“ r := by exact IsOpen.mem_nhds hs hr
  have hsum : âˆ€ t âˆˆ s, Summable fun (n : Î¹) => f n t := fun t ht =>
    by simpa using SummableLocallyUniformlyOn.summable (h 0 (zero_le N)) ht
  constructor
  Â· intro m hm
    refine ContinuousOn.congr (f := fun x => âˆ‘' n, iteratedDerivWithin m (f n) s x) ?_ ?_
    Â· exact SummableLocallyUniformlyOn.continuousOn_tsum (fun i => (hf i).1 m hm) (h m hm)
    Â· intro x hx
      refine SummableLocallyUniformlyOn.iteratedDerivWithin_tsum m hs hx hsum ?_ ?_
      Â· intro k _ hk
        have : k â‰¤ N := LE.le.trans (mod_cast hk) hm
        exact h k this
      Â· intro n k r hk hr
        have p : k < N := lt_of_lt_of_le (mod_cast hk) hm
        exact ((hf n).2 k p).differentiableAt (q r hr)
  Â· intro m hm
    have h'm : ((m + 1 : â„•) : â„•âˆ) â‰¤ N := by
      simpa only [ENat.coe_add, ENat.coe_one] using Order.add_one_le_of_lt hm
    refine DifferentiableOn.congr (f := fun x => âˆ‘' n, iteratedDerivWithin m (f n) s x) ?_ ?_
    Â· apply SummableLocallyUniformlyOn.differentiableOn_tsum hs ?_ ?_ ?_
      Â· intro y hy
        exact SummableLocallyUniformlyOn.summable (h m hm.le) hy
      Â· apply SummableLocallyUniformlyOn_congr (f := fun n => iteratedDerivWithin (m + 1) (f n) s)
        Â· intro n x hx; rw [â† iteratedDerivWithin_succ]
        Â· exact h (m + 1) h'm
      Â· intro n r hr
        exact ((hf n).2 m hm).differentiableAt (q r hr)
    Â· intro x hx
      rw [SummableLocallyUniformlyOn.iteratedDerivWithin_tsum m hs hx hsum]
      Â· intro k _ hk
        have : k â‰¤ N := LE.le.trans (mod_cast hk) hm.le
        exact h k this
      Â· intro n k r hk hr
        have p : k < N := LT.lt.trans (mod_cast hk) hm
        exact ((hf n).2 k p).differentiableAt (q r hr)

end Differentiable
