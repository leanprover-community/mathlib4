/-
Copyright (c) 2025 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/
module

public import Mathlib.Analysis.Calculus.IteratedDeriv.Defs
public import Mathlib.Analysis.Calculus.UniformLimitsDeriv
public import Mathlib.Analysis.Normed.Group.FunctionSeries
public import Mathlib.Topology.Algebra.InfiniteSum.UniformOn

/-!
# Differentiability of sum of functions

We prove some `HasSumUniformlyOn` versions of theorems from
`Mathlib.Analysis.NormedSpace.FunctionSeries`.

Alongside this we prove `derivWithin_tsum` which states that the derivative of a series of functions
is the sum of the derivatives, under suitable conditions we also prove an `iteratedDerivWithin`
version.

-/

@[expose] public section

open Set Metric TopologicalSpace Function Filter

open scoped Topology NNReal

section UniformlyOn

variable {Î± Î² F : Type*} [NormedAddCommGroup F] [CompleteSpace F] {u : Î± â†’ â„}

theorem HasSumUniformlyOn.of_norm_le_summable {f : Î± â†’ Î² â†’ F} (hu : Summable u) {s : Set Î²}
    (hfu : âˆ€ n x, x âˆˆ s â†’ â€–f n xâ€– â‰¤ u n) : HasSumUniformlyOn f (fun x â†¦ âˆ‘' n, f n x) s := by
  simp [hasSumUniformlyOn_iff_tendstoUniformlyOn, tendstoUniformlyOn_tsum hu hfu]

theorem HasSumUniformlyOn.of_norm_le_summable_eventually {Î¹ : Type*} {f : Î¹ â†’ Î² â†’ F} {u : Î¹ â†’ â„}
    (hu : Summable u) {s : Set Î²} (hfu : âˆ€á¶  n in cofinite, âˆ€ x âˆˆ s, â€–f n xâ€– â‰¤ u n) :
    HasSumUniformlyOn f (fun x â†¦ âˆ‘' n, f n x) s := by
  simp [hasSumUniformlyOn_iff_tendstoUniformlyOn,
    tendstoUniformlyOn_tsum_of_cofinite_eventually hu hfu]

lemma SummableLocallyUniformlyOn.of_locally_bounded_eventually [TopologicalSpace Î²]
    [LocallyCompactSpace Î²] {f : Î± â†’ Î² â†’ F} {s : Set Î²} (hs : IsOpen s)
    (hu : âˆ€ K âŠ† s, IsCompact K â†’ âˆƒ u : Î± â†’ â„, Summable u âˆ§
    âˆ€á¶  n in cofinite, âˆ€ k âˆˆ K, â€–f n kâ€– â‰¤ u n) : SummableLocallyUniformlyOn f s := by
  apply HasSumLocallyUniformlyOn.summableLocallyUniformlyOn (g := fun x â†¦ âˆ‘' n, f n x)
  rw [hasSumLocallyUniformlyOn_iff_tendstoLocallyUniformlyOn,
    tendstoLocallyUniformlyOn_iff_forall_isCompact hs]
  intro K hK hKc
  obtain âŸ¨u, hu1, hu2âŸ© := hu K hK hKc
  exact tendstoUniformlyOn_tsum_of_cofinite_eventually hu1 hu2

lemma SummableLocallyUniformlyOn_of_locally_bounded [TopologicalSpace Î²] [LocallyCompactSpace Î²]
    {f : Î± â†’ Î² â†’ F} {s : Set Î²} (hs : IsOpen s)
    (hu : âˆ€ K âŠ† s, IsCompact K â†’ âˆƒ u : Î± â†’ â„, Summable u âˆ§ âˆ€ n, âˆ€ k âˆˆ K, â€–f n kâ€– â‰¤ u n) :
    SummableLocallyUniformlyOn f s := by
  apply SummableLocallyUniformlyOn.of_locally_bounded_eventually hs
  intro K hK hKc
  obtain âŸ¨u, hu1, hu2âŸ© := hu K hK hKc
  exact âŸ¨u, hu1, by filter_upwards using hu2âŸ©

end UniformlyOn

variable {Î¹ ğ•œ F : Type*} [NontriviallyNormedField ğ•œ] [IsRCLikeNormedField ğ•œ]
    [NormedAddCommGroup F] [NormedSpace ğ•œ F] {s : Set ğ•œ}

/-- The `derivWithin` of a sum whose derivative is absolutely and uniformly convergent sum on an
open set `s` is the sum of the derivatives of sequence of functions on the open set `s` -/
theorem derivWithin_tsum {f : Î¹ â†’ ğ•œ â†’ F} (hs : IsOpen s) {x : ğ•œ} (hx : x âˆˆ s)
    (hf : âˆ€ y âˆˆ s, Summable fun n â†¦ f n y)
    (h : SummableLocallyUniformlyOn (fun n â†¦ (derivWithin (fun z â†¦ f n z) s)) s)
    (hf2 : âˆ€ n r, r âˆˆ s â†’ DifferentiableAt ğ•œ (f n) r) :
    derivWithin (fun z â†¦ âˆ‘' n, f n z) s x = âˆ‘' n, derivWithin (f n) s x := by
  apply HasDerivWithinAt.derivWithin ?_ (hs.uniqueDiffWithinAt hx)
  apply HasDerivAt.hasDerivWithinAt
  apply hasDerivAt_of_tendstoLocallyUniformlyOn hs _ _ (fun y hy â†¦ (hf y hy).hasSum) hx
    (f' := fun n : Finset Î¹ â†¦ fun a â†¦ âˆ‘ i âˆˆ n, derivWithin (fun z â†¦ f i z) s a)
  Â· obtain âŸ¨g, hgâŸ© := h
    apply (hasSumLocallyUniformlyOn_iff_tendstoLocallyUniformlyOn.mp hg).congr_right
    exact fun _ hb â†¦ (hg.tsum_eqOn hb).symm
  Â· filter_upwards with t r hr using HasDerivAt.fun_sum
      (fun q hq â†¦ ((hf2 q r hr).differentiableWithinAt.hasDerivWithinAt.hasDerivAt)
      (hs.mem_nhds hr))

/-- If a sequence of functions `fâ‚™` is such that `âˆ‘ fâ‚™ (z)` is summable for each `z` in an
open set `s`, and for each `1 â‰¤ k â‰¤ m`, the series of `k`-th iterated derivatives
`âˆ‘ (iteratedDerivWithin k fâ‚™ s) (z)`
is summable locally uniformly on `s`, and each `fâ‚™` is `m`-times differentiable, then the `m`-th
iterated derivative of the sum is the sum of the `m`-th iterated derivatives. -/
theorem iteratedDerivWithin_tsum {f : Î¹ â†’ ğ•œ â†’ F} (m : â„•) (hs : IsOpen s)
    {x : ğ•œ} (hx : x âˆˆ s) (hsum : âˆ€ t âˆˆ s, Summable (fun n : Î¹ â†¦ f n t))
    (h : âˆ€ k, 1 â‰¤ k â†’ k â‰¤ m â†’ SummableLocallyUniformlyOn
      (fun n â†¦ (iteratedDerivWithin k (fun z â†¦ f n z) s)) s)
    (hf2 : âˆ€ n k r, k â‰¤ m â†’ r âˆˆ s â†’
      DifferentiableAt ğ•œ (iteratedDerivWithin k (fun z â†¦ f n z) s) r) :
    iteratedDerivWithin m (fun z â†¦ âˆ‘' n, f n z) s x = âˆ‘' n, iteratedDerivWithin m (f n) s x := by
  induction m generalizing x with
  | zero => simp
  | succ m hm =>
    simp_rw [iteratedDerivWithin_succ]
    rw [â† derivWithin_tsum hs hx _ _ (fun n r hr â†¦ hf2 n m r (by lia) hr)]
    Â· exact derivWithin_congr (fun t ht â†¦ hm ht (fun k hk1 hkm â†¦ h k hk1 (by lia))
          (fun k r e hr he â†¦ hf2 k r e (by lia) he)) (hm hx (fun k hk1 hkm â†¦ h k hk1 (by lia))
          (fun k r e hr he â†¦ hf2 k r e (by lia) he))
    Â· intro r hr
      by_cases hm2 : m = 0
      Â· simp [hm2, hsum r hr]
      Â· exact ((h m (by lia) (by lia)).summable hr).congr (fun _ â†¦ by simp)
    Â· exact SummableLocallyUniformlyOn_congr
        (fun _ _ ht â†¦ iteratedDerivWithin_succ) (h (m + 1) (by lia) (by lia))
