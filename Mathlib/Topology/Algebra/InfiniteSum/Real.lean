/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Yury Kudryashov
-/
import Mathlib.Algebra.BigOperators.Intervals
import Mathlib.Topology.Algebra.InfiniteSum.Order
import Mathlib.Topology.Instances.Real
import Mathlib.Topology.Instances.ENNReal

#align_import topology.algebra.infinite_sum.real from "leanprover-community/mathlib"@"9a59dcb7a2d06bf55da57b9030169219980660cd"

/-!
# Infinite sum in the reals

This file provides lemmas about Cauchy sequences in terms of infinite sums and infinite sums valued
in the reals.
-/

open Filter Finset BigOperators NNReal Topology

variable {Œ± Œ≤ : Type*} [PseudoMetricSpace Œ±] {f : ‚Ñï ‚Üí Œ±} {a : Œ±}

/-- If the distance between consecutive points of a sequence is estimated by a summable series,
then the original sequence is a Cauchy sequence. -/
theorem cauchySeq_of_dist_le_of_summable (d : ‚Ñï ‚Üí ‚Ñù) (hf : ‚àÄ n, dist (f n) (f n.succ) ‚â§ d n)
    (hd : Summable d) : CauchySeq f := by
  -- Porting note (#11215): TODO: with `Topology.Instances.NNReal` we can use this:
  -- lift d to ‚Ñï ‚Üí ‚Ñù‚â•0 using fun n ‚Ü¶ dist_nonneg.trans (hf n)
  -- refine cauchySeq_of_edist_le_of_summable d ?_ ?_
  -- ¬∑ exact_mod_cast hf
  -- ¬∑ exact_mod_cast hd
  refine' Metric.cauchySeq_iff'.2 fun Œµ Œµpos ‚Ü¶ _
  replace hd : CauchySeq fun n : ‚Ñï ‚Ü¶ ‚àë x in range n, d x :=
    let ‚ü®_, H‚ü© := hd
    H.tendsto_sum_nat.cauchySeq
  refine' (Metric.cauchySeq_iff'.1 hd Œµ Œµpos).imp fun N hN n hn ‚Ü¶ _
  have hsum := hN n hn
  rw [Real.dist_eq, ‚Üê sum_Ico_eq_sub _ hn] at hsum
  calc
    dist (f n) (f N) = dist (f N) (f n) := dist_comm _ _
    _ ‚â§ ‚àë x in Ico N n, d x := dist_le_Ico_sum_of_dist_le hn fun _ _ ‚Ü¶ hf _
    _ ‚â§ |‚àë x in Ico N n, d x| := le_abs_self _
    _ < Œµ := hsum
#align cauchy_seq_of_dist_le_of_summable cauchySeq_of_dist_le_of_summable

theorem cauchySeq_of_summable_dist (h : Summable fun n ‚Ü¶ dist (f n) (f n.succ)) : CauchySeq f :=
  cauchySeq_of_dist_le_of_summable _ (fun _ ‚Ü¶ le_rfl) h
#align cauchy_seq_of_summable_dist cauchySeq_of_summable_dist

theorem dist_le_tsum_of_dist_le_of_tendsto (d : ‚Ñï ‚Üí ‚Ñù) (hf : ‚àÄ n, dist (f n) (f n.succ) ‚â§ d n)
    (hd : Summable d) {a : Œ±} (ha : Tendsto f atTop (ùìù a)) (n : ‚Ñï) :
    dist (f n) a ‚â§ ‚àë' m, d (n + m) := by
  refine' le_of_tendsto (tendsto_const_nhds.dist ha) (eventually_atTop.2 ‚ü®n, fun m hnm ‚Ü¶ _‚ü©)
  refine' le_trans (dist_le_Ico_sum_of_dist_le hnm fun _ _ ‚Ü¶ hf _) _
  rw [sum_Ico_eq_sum_range]
  refine' sum_le_tsum (range _) (fun _ _ ‚Ü¶ le_trans dist_nonneg (hf _)) _
  exact hd.comp_injective (add_right_injective n)
#align dist_le_tsum_of_dist_le_of_tendsto dist_le_tsum_of_dist_le_of_tendsto

theorem dist_le_tsum_of_dist_le_of_tendsto‚ÇÄ (d : ‚Ñï ‚Üí ‚Ñù) (hf : ‚àÄ n, dist (f n) (f n.succ) ‚â§ d n)
    (hd : Summable d) (ha : Tendsto f atTop (ùìù a)) : dist (f 0) a ‚â§ tsum d := by
  simpa only [zero_add] using dist_le_tsum_of_dist_le_of_tendsto d hf hd ha 0
#align dist_le_tsum_of_dist_le_of_tendsto‚ÇÄ dist_le_tsum_of_dist_le_of_tendsto‚ÇÄ

theorem dist_le_tsum_dist_of_tendsto (h : Summable fun n ‚Ü¶ dist (f n) (f n.succ))
    (ha : Tendsto f atTop (ùìù a)) (n) : dist (f n) a ‚â§ ‚àë' m, dist (f (n + m)) (f (n + m).succ) :=
  show dist (f n) a ‚â§ ‚àë' m, (fun x ‚Ü¶ dist (f x) (f x.succ)) (n + m) from
    dist_le_tsum_of_dist_le_of_tendsto (fun n ‚Ü¶ dist (f n) (f n.succ)) (fun _ ‚Ü¶ le_rfl) h ha n
#align dist_le_tsum_dist_of_tendsto dist_le_tsum_dist_of_tendsto

theorem dist_le_tsum_dist_of_tendsto‚ÇÄ (h : Summable fun n ‚Ü¶ dist (f n) (f n.succ))
    (ha : Tendsto f atTop (ùìù a)) : dist (f 0) a ‚â§ ‚àë' n, dist (f n) (f n.succ) := by
  simpa only [zero_add] using dist_le_tsum_dist_of_tendsto h ha 0
#align dist_le_tsum_dist_of_tendsto‚ÇÄ dist_le_tsum_dist_of_tendsto‚ÇÄ

section summable

theorem not_summable_iff_tendsto_nat_atTop_of_nonneg {f : ‚Ñï ‚Üí ‚Ñù} (hf : ‚àÄ n, 0 ‚â§ f n) :
    ¬¨Summable f ‚Üî Tendsto (fun n : ‚Ñï => ‚àë i in Finset.range n, f i) atTop atTop := by
  lift f to ‚Ñï ‚Üí ‚Ñù‚â•0 using hf
  exact mod_cast NNReal.not_summable_iff_tendsto_nat_atTop
#align not_summable_iff_tendsto_nat_at_top_of_nonneg not_summable_iff_tendsto_nat_atTop_of_nonneg

theorem summable_iff_not_tendsto_nat_atTop_of_nonneg {f : ‚Ñï ‚Üí ‚Ñù} (hf : ‚àÄ n, 0 ‚â§ f n) :
    Summable f ‚Üî ¬¨Tendsto (fun n : ‚Ñï => ‚àë i in Finset.range n, f i) atTop atTop := by
  rw [‚Üê not_iff_not, Classical.not_not, not_summable_iff_tendsto_nat_atTop_of_nonneg hf]
#align summable_iff_not_tendsto_nat_at_top_of_nonneg summable_iff_not_tendsto_nat_atTop_of_nonneg

theorem summable_sigma_of_nonneg {Œ≤ : Œ± ‚Üí Type*} {f : (Œ£ x, Œ≤ x) ‚Üí ‚Ñù} (hf : ‚àÄ x, 0 ‚â§ f x) :
    Summable f ‚Üî (‚àÄ x, Summable fun y => f ‚ü®x, y‚ü©) ‚àß Summable fun x => ‚àë' y, f ‚ü®x, y‚ü© := by
  lift f to (Œ£x, Œ≤ x) ‚Üí ‚Ñù‚â•0 using hf
  exact mod_cast NNReal.summable_sigma
#align summable_sigma_of_nonneg summable_sigma_of_nonneg

lemma summable_partition {Œ∫ Œπ: Type*} {f : Œπ ‚Üí ‚Ñù} (hf : 0 ‚â§ f) {s : Œ∫ ‚Üí Set Œπ}
    (hs : ‚àÄ i, ‚àÉ! j, i ‚àà s j) : Summable f ‚Üî
      (‚àÄ j, Summable fun i : s j ‚Ü¶ f i) ‚àß Summable fun j ‚Ü¶ ‚àë' i : s j, f i := by
  rw [‚Üê (Set.sigmaEquiv s hs).summable_iff, summable_sigma_of_nonneg]
  simp only [Set.sigmaEquiv, Equiv.coe_fn_mk, Function.comp_apply]
  exact fun _ ‚Ü¶ hf _

theorem summable_prod_of_nonneg {f : (Œ± √ó Œ≤) ‚Üí ‚Ñù} (hf : 0 ‚â§ f) :
    Summable f ‚Üî (‚àÄ x, Summable fun y ‚Ü¶ f (x, y)) ‚àß Summable fun x ‚Ü¶ ‚àë' y, f (x, y) :=
  (Equiv.sigmaEquivProd _ _).summable_iff.symm.trans <| summable_sigma_of_nonneg fun _ ‚Ü¶ hf _

theorem summable_of_sum_le {Œπ : Type*} {f : Œπ ‚Üí ‚Ñù} {c : ‚Ñù} (hf : 0 ‚â§ f)
    (h : ‚àÄ u : Finset Œπ, ‚àë x in u, f x ‚â§ c) : Summable f :=
  ‚ü®‚®Ü u : Finset Œπ, ‚àë x in u, f x,
    tendsto_atTop_ciSup (Finset.sum_mono_set_of_nonneg hf) ‚ü®c, fun _ ‚ü®u, hu‚ü© => hu ‚ñ∏ h u‚ü©‚ü©
#align summable_of_sum_le summable_of_sum_le

theorem summable_of_sum_range_le {f : ‚Ñï ‚Üí ‚Ñù} {c : ‚Ñù} (hf : ‚àÄ n, 0 ‚â§ f n)
    (h : ‚àÄ n, ‚àë i in Finset.range n, f i ‚â§ c) : Summable f := by
  refine (summable_iff_not_tendsto_nat_atTop_of_nonneg hf).2 fun H => ?_
  rcases exists_lt_of_tendsto_atTop H 0 c with ‚ü®n, -, hn‚ü©
  exact lt_irrefl _ (hn.trans_le (h n))
#align summable_of_sum_range_le summable_of_sum_range_le

theorem Real.tsum_le_of_sum_range_le {f : ‚Ñï ‚Üí ‚Ñù} {c : ‚Ñù} (hf : ‚àÄ n, 0 ‚â§ f n)
    (h : ‚àÄ n, ‚àë i in Finset.range n, f i ‚â§ c) : ‚àë' n, f n ‚â§ c :=
  _root_.tsum_le_of_sum_range_le (summable_of_sum_range_le hf h) h
#align real.tsum_le_of_sum_range_le Real.tsum_le_of_sum_range_le

/-- If a sequence `f` with non-negative terms is dominated by a sequence `g` with summable
series and at least one term of `f` is strictly smaller than the corresponding term in `g`,
then the series of `f` is strictly smaller than the series of `g`. -/
theorem tsum_lt_tsum_of_nonneg {i : ‚Ñï} {f g : ‚Ñï ‚Üí ‚Ñù} (h0 : ‚àÄ b : ‚Ñï, 0 ‚â§ f b)
    (h : ‚àÄ b : ‚Ñï, f b ‚â§ g b) (hi : f i < g i) (hg : Summable g) : ‚àë' n, f n < ‚àë' n, g n :=
  tsum_lt_tsum h hi (.of_nonneg_of_le h0 h hg) hg
#align tsum_lt_tsum_of_nonneg tsum_lt_tsum_of_nonneg

end summable
