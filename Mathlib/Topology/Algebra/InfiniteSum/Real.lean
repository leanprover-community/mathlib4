/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
import Mathlib.Algebra.BigOperators.Intervals
import Mathlib.Topology.Algebra.InfiniteSum.Order
import Mathlib.Topology.Instances.Real
import Mathlib.Topology.Instances.ENNReal

#align_import topology.algebra.infinite_sum.real from "leanprover-community/mathlib"@"9a59dcb7a2d06bf55da57b9030169219980660cd"

/-!
# Infinite sum in the reals

This file provides lemmas about Cauchy sequences in terms of infinite sums and infinite sums valued
in the reals.
-/

open Filter Finset NNReal Topology

variable {Î± Î² : Type*} [PseudoMetricSpace Î±] {f : â„• â†’ Î±} {a : Î±}

/-- If the distance between consecutive points of a sequence is estimated by a summable series,
then the original sequence is a Cauchy sequence. -/
theorem cauchySeq_of_dist_le_of_summable (d : â„• â†’ â„) (hf : âˆ€ n, dist (f n) (f n.succ) â‰¤ d n)
    (hd : Summable d) : CauchySeq f := by
  lift d to â„• â†’ â„â‰¥0 using fun n â†¦ dist_nonneg.trans (hf n)
  apply cauchySeq_of_edist_le_of_summable d (Î± := Î±) (f := f)
  Â· exact_mod_cast hf
  Â· exact_mod_cast hd

#align cauchy_seq_of_dist_le_of_summable cauchySeq_of_dist_le_of_summable

theorem cauchySeq_of_summable_dist (h : Summable fun n â†¦ dist (f n) (f n.succ)) : CauchySeq f :=
  cauchySeq_of_dist_le_of_summable _ (fun _ â†¦ le_rfl) h
#align cauchy_seq_of_summable_dist cauchySeq_of_summable_dist

theorem dist_le_tsum_of_dist_le_of_tendsto (d : â„• â†’ â„) (hf : âˆ€ n, dist (f n) (f n.succ) â‰¤ d n)
    (hd : Summable d) {a : Î±} (ha : Tendsto f atTop (ğ“ a)) (n : â„•) :
    dist (f n) a â‰¤ âˆ‘' m, d (n + m) := by
  refine le_of_tendsto (tendsto_const_nhds.dist ha) (eventually_atTop.2 âŸ¨n, fun m hnm â†¦ ?_âŸ©)
  refine le_trans (dist_le_Ico_sum_of_dist_le hnm fun _ _ â†¦ hf _) ?_
  rw [sum_Ico_eq_sum_range]
  refine sum_le_tsum (range _) (fun _ _ â†¦ le_trans dist_nonneg (hf _)) ?_
  exact hd.comp_injective (add_right_injective n)
#align dist_le_tsum_of_dist_le_of_tendsto dist_le_tsum_of_dist_le_of_tendsto

theorem dist_le_tsum_of_dist_le_of_tendstoâ‚€ (d : â„• â†’ â„) (hf : âˆ€ n, dist (f n) (f n.succ) â‰¤ d n)
    (hd : Summable d) (ha : Tendsto f atTop (ğ“ a)) : dist (f 0) a â‰¤ tsum d := by
  simpa only [zero_add] using dist_le_tsum_of_dist_le_of_tendsto d hf hd ha 0
#align dist_le_tsum_of_dist_le_of_tendstoâ‚€ dist_le_tsum_of_dist_le_of_tendstoâ‚€

theorem dist_le_tsum_dist_of_tendsto (h : Summable fun n â†¦ dist (f n) (f n.succ))
    (ha : Tendsto f atTop (ğ“ a)) (n) : dist (f n) a â‰¤ âˆ‘' m, dist (f (n + m)) (f (n + m).succ) :=
  show dist (f n) a â‰¤ âˆ‘' m, (fun x â†¦ dist (f x) (f x.succ)) (n + m) from
    dist_le_tsum_of_dist_le_of_tendsto (fun n â†¦ dist (f n) (f n.succ)) (fun _ â†¦ le_rfl) h ha n
#align dist_le_tsum_dist_of_tendsto dist_le_tsum_dist_of_tendsto

theorem dist_le_tsum_dist_of_tendstoâ‚€ (h : Summable fun n â†¦ dist (f n) (f n.succ))
    (ha : Tendsto f atTop (ğ“ a)) : dist (f 0) a â‰¤ âˆ‘' n, dist (f n) (f n.succ) := by
  simpa only [zero_add] using dist_le_tsum_dist_of_tendsto h ha 0
#align dist_le_tsum_dist_of_tendstoâ‚€ dist_le_tsum_dist_of_tendstoâ‚€

section summable

theorem not_summable_iff_tendsto_nat_atTop_of_nonneg {f : â„• â†’ â„} (hf : âˆ€ n, 0 â‰¤ f n) :
    Â¬Summable f â†” Tendsto (fun n : â„• => âˆ‘ i âˆˆ Finset.range n, f i) atTop atTop := by
  lift f to â„• â†’ â„â‰¥0 using hf
  exact mod_cast NNReal.not_summable_iff_tendsto_nat_atTop
#align not_summable_iff_tendsto_nat_at_top_of_nonneg not_summable_iff_tendsto_nat_atTop_of_nonneg

theorem summable_iff_not_tendsto_nat_atTop_of_nonneg {f : â„• â†’ â„} (hf : âˆ€ n, 0 â‰¤ f n) :
    Summable f â†” Â¬Tendsto (fun n : â„• => âˆ‘ i âˆˆ Finset.range n, f i) atTop atTop := by
  rw [â† not_iff_not, Classical.not_not, not_summable_iff_tendsto_nat_atTop_of_nonneg hf]
#align summable_iff_not_tendsto_nat_at_top_of_nonneg summable_iff_not_tendsto_nat_atTop_of_nonneg

theorem summable_sigma_of_nonneg {Î² : Î± â†’ Type*} {f : (Î£ x, Î² x) â†’ â„} (hf : âˆ€ x, 0 â‰¤ f x) :
    Summable f â†” (âˆ€ x, Summable fun y => f âŸ¨x, yâŸ©) âˆ§ Summable fun x => âˆ‘' y, f âŸ¨x, yâŸ© := by
  lift f to (Î£x, Î² x) â†’ â„â‰¥0 using hf
  exact mod_cast NNReal.summable_sigma
#align summable_sigma_of_nonneg summable_sigma_of_nonneg

lemma summable_partition {Î± Î² : Type*} {f : Î² â†’ â„} (hf : 0 â‰¤ f) {s : Î±  â†’ Set Î²}
    (hs : âˆ€ i, âˆƒ! j, i âˆˆ s j) : Summable f â†”
      (âˆ€ j, Summable fun i : s j â†¦ f i) âˆ§ Summable fun j â†¦ âˆ‘' i : s j, f i := by
  simpa only [â† (Set.sigmaEquiv s hs).summable_iff] using summable_sigma_of_nonneg (fun _ â†¦ hf _)

theorem summable_prod_of_nonneg {f : (Î± Ã— Î²) â†’ â„} (hf : 0 â‰¤ f) :
    Summable f â†” (âˆ€ x, Summable fun y â†¦ f (x, y)) âˆ§ Summable fun x â†¦ âˆ‘' y, f (x, y) :=
  (Equiv.sigmaEquivProd _ _).summable_iff.symm.trans <| summable_sigma_of_nonneg fun _ â†¦ hf _

theorem summable_of_sum_le {Î¹ : Type*} {f : Î¹ â†’ â„} {c : â„} (hf : 0 â‰¤ f)
    (h : âˆ€ u : Finset Î¹, âˆ‘ x âˆˆ u, f x â‰¤ c) : Summable f :=
  âŸ¨â¨† u : Finset Î¹, âˆ‘ x âˆˆ u, f x,
    tendsto_atTop_ciSup (Finset.sum_mono_set_of_nonneg hf) âŸ¨c, fun _ âŸ¨u, huâŸ© => hu â–¸ h uâŸ©âŸ©
#align summable_of_sum_le summable_of_sum_le

theorem summable_of_sum_range_le {f : â„• â†’ â„} {c : â„} (hf : âˆ€ n, 0 â‰¤ f n)
    (h : âˆ€ n, âˆ‘ i âˆˆ Finset.range n, f i â‰¤ c) : Summable f := by
  refine (summable_iff_not_tendsto_nat_atTop_of_nonneg hf).2 fun H => ?_
  rcases exists_lt_of_tendsto_atTop H 0 c with âŸ¨n, -, hnâŸ©
  exact lt_irrefl _ (hn.trans_le (h n))
#align summable_of_sum_range_le summable_of_sum_range_le

theorem Real.tsum_le_of_sum_range_le {f : â„• â†’ â„} {c : â„} (hf : âˆ€ n, 0 â‰¤ f n)
    (h : âˆ€ n, âˆ‘ i âˆˆ Finset.range n, f i â‰¤ c) : âˆ‘' n, f n â‰¤ c :=
  _root_.tsum_le_of_sum_range_le (summable_of_sum_range_le hf h) h
#align real.tsum_le_of_sum_range_le Real.tsum_le_of_sum_range_le

/-- If a sequence `f` with non-negative terms is dominated by a sequence `g` with summable
series and at least one term of `f` is strictly smaller than the corresponding term in `g`,
then the series of `f` is strictly smaller than the series of `g`. -/
theorem tsum_lt_tsum_of_nonneg {i : â„•} {f g : â„• â†’ â„} (h0 : âˆ€ b : â„•, 0 â‰¤ f b)
    (h : âˆ€ b : â„•, f b â‰¤ g b) (hi : f i < g i) (hg : Summable g) : âˆ‘' n, f n < âˆ‘' n, g n :=
  tsum_lt_tsum h hi (.of_nonneg_of_le h0 h hg) hg
#align tsum_lt_tsum_of_nonneg tsum_lt_tsum_of_nonneg

end summable
