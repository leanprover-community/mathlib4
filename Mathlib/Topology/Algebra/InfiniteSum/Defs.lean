/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl
-/
import Mathlib.Algebra.BigOperators.Finprod
import Mathlib.Order.Filter.AtTopBot.BigOperators
import Mathlib.Topology.Algebra.InfiniteSum.SummationFilter
import Mathlib.Topology.Separation.Hausdorff

/-!
# Infinite sum and product in a topological monoid

This file defines infinite products and sums for (possibly infinite) indexed families of elements
in a commutative topological monoid (resp. add monoid).

To handle convergence questions we use the formalism of *summation filters* (defined in the
file `Mathlib.Topology.Algebra.InfiniteSum.SummationFilter`). These are filters on the finite
subsets of a given type, and we define a function to be *summable* for a summation filter `L` if
its partial sums over finite subsets tend to a limit along `L` (and similarly for products).

This simultaneously generalizes several different kinds of summation: for instance,
*unconditional summation* (which makes sense for any index type) where we take the limit with
respect to the `atTop` filter; but also *conditional summation* for functions on `â„•`, where the
limit is over the partial sums `âˆ‘ i âˆˆ range n, f i` as `n â†’ âˆ` (so there exist
conditionally-summable sequences which are not unconditionally summable).

## Implementation notes

We say that a function `f : Î² â†’ Î±` has a product of `a` w.r.t. the summation filter `L` if the
function `fun s : Finset Î² â†¦ âˆ b âˆˆ s, f b` converges to `a` w.r.t. the filter `L.filter` on
`Finset Î²`.

In the most important case of unconditional summation, this translates to the following condition:
for every neighborhood `U` of `a`, there exists a finite set `s : Finset Î²` of indices such
that `âˆ b âˆˆ s', f b âˆˆ U` for any finite set `s'` which is a superset of `s`.

This may yield some unexpected results. For example, according to this definition, the product
`âˆ' n : â„•, (1 : â„) / 2` unconditionally exists and is equal to `0`. More strikingly,
the product `âˆ' n : â„•, (n : â„)` unconditionally exists and is equal to `0`, because one
of its terms is `0` (even though the product of the remaining terms diverges). Users who would
prefer that these products be considered not to exist can carry them out in the unit group `â„Ë£`
rather than in `â„`.

## References

* Bourbaki: General Topology (1995), Chapter 3 Â§5 (Infinite sums in commutative groups)

-/

/- **NOTE**. This file is intended to be kept short, just enough to state the basic definitions and
six key lemmas relating them together, namely `Summable.hasSum`, `Multipliable.hasProd`,
`HasSum.tsum_eq`, `HasProd.tprod_eq`, `Summable.hasSum_iff`, and `Multipliable.hasProd_iff`.

Do not add further lemmas here -- add them to `InfiniteSum.Basic` or (preferably) another, more
specific file. -/

noncomputable section

open Filter Function

open scoped Topology

variable {Î± Î² Î³ : Type*}

section HasProd

variable [CommMonoid Î±] [TopologicalSpace Î±]

/-- `HasProd f a L` means that the (potentially infinite) product of the `f b` for `b : Î²` converges
to `a` along the SummationFilter `L`.

By default `L` is the `unconditional` one, corresponding to the limit of all finite sets towards
the entire type. So we take the product over bigger and bigger finite sets. This product operation
is invariant under permuting the terms (while products for more general summation filters usually
are not).

For the definition and many statements, `Î±` does not need to be a topological monoid, only a monoid
with a topology (i.e. the multiplication is not assumed to be continuous). We only add this
assumption later, for the lemmas where it is relevant.

These are defined in an identical way to infinite sums (`HasSum`). For example, we say that
the function `â„• â†’ â„` sending `n` to `1 / 2` has a product of `0`, rather than saying that it does
not converge as some authors would. -/
@[to_additive /-- `HasSum f a L` means that the (potentially infinite) sum of the `f b` for `b : Î²`
converges to `a` along the SummationFilter `L``.

By default `L` is the `unconditional` one, corresponding to the limit of all finite sets towards
the entire type. So we take the sum over bigger and bigger finite sets. This sum operation is
invariant under permuting the terms (while sums for more general summation filters usually are not).
This is based on Mario Carneiro's
[infinite sum `df-tsms` in Metamath](http://us.metamath.org/mpeuni/df-tsms.html).

In particular, the function `â„• â†’ â„` sending `n` to `(-1) ^ n / (n + 1)` does not have a
sum for this definition, although it is summable for the `conditional` summation filter that
takes limits of sums over `n âˆˆ {0, ..., X}` as `X â†’ âˆ`. However, a series which is *absolutely*
convergent with respect to the conditional summation filter is in fact unconditionally summable.

For the definition and many statements, `Î±` does not need to be a topological additive monoid,
only an additive monoid with a topology (i.e. the addition is not assumed to be continuous). We
only add this assumption later, for the lemmas where it is relevant. -/]
def HasProd (f : Î² â†’ Î±) (a : Î±) (L := unconditional Î²) : Prop :=
  Tendsto (fun s : Finset Î² â†¦ âˆ b âˆˆ s, f b) L.filter (ğ“ a)

/-- `Multipliable f` means that `f` has some (infinite) product with respect to `L`. Use `tprod` to
get the value. -/
@[to_additive
/-- `Summable f` means that `f` has some (infinite) sum with respect to `L`. Use `tsum` to get the
value. -/]
def Multipliable (f : Î² â†’ Î±) (L := unconditional Î²) : Prop :=
  âˆƒ a, HasProd f a L

@[to_additive]
lemma Multipliable.mono_filter {f : Î² â†’ Î±} {Lâ‚ Lâ‚‚ : SummationFilter Î²}
    (hf : Multipliable f Lâ‚‚) (h : Lâ‚.filter â‰¤ Lâ‚‚.filter) : Multipliable f Lâ‚ :=
  match hf with | âŸ¨a, haâŸ© => âŸ¨a, ha.mono_left hâŸ©

open scoped Classical in
/-- `âˆ' i, f i` is the unconditional product of `f`, if it exists, or 1 otherwise. ]

More generally, if `L` is a `SummationFilter`, `âˆ'[L] i, f i` is the product of `f` with respect to
`L` if it exists, and `1` otherwise.

(Note that even if the unconditional product exists, it might not be unique if the topology is not
separated. When the multiplicative support of `f` is finite, we make the most reasonable choice,
to use the product over the multiplicative support. Otherwise, we choose arbitrarily an `a`
satisfying `HasProd f a`. Similar remarks apply to more general summation filters.) -/
@[to_additive /-- `âˆ‘' i, f i` is the unconditional sum of `f` if it exists, or 0 otherwise.

More generally, if `L` is a `SummationFilter`, `âˆ‘'[L] i, f i` is the sum of `f` with respect to
`L` if it exists, and `1` otherwise.

(Note that even if the unconditional sum exists, it might not be unique if the topology is not
separated. When the support of `f` is finite, we make the most reasonable choice, to use the product
over the support. Otherwise, we choose arbitrarily an `a` satisfying `HasProd f a`. Similar remarks
apply to more general summation filters.)
-/]
noncomputable irreducible_def tprod (f : Î² â†’ Î±) (L := unconditional Î²) :=
  if h : Multipliable f L then
    if L.HasSupport âˆ§ (mulSupport f âˆ© L.support).Finite then finprod (L.support.mulIndicator f)
    else h.choose
  else 1

variable {L : SummationFilter Î²}

@[inherit_doc tprod]
notation3 "âˆ'[" L "]" (...)", "r:67:(scoped f => tprod f L) => r
@[inherit_doc tsum]
notation3 "âˆ‘'[" L "]" (...)", "r:67:(scoped f => tsum f L) => r

-- see Note [operator precedence of big operators]
@[inherit_doc tprod]
notation3 "âˆ' "(...)", "r:67:(scoped f => tprod f (unconditional _)) => r
@[inherit_doc tsum]
notation3 "âˆ‘' "(...)", "r:67:(scoped f => tsum f (unconditional _)) => r

variable {f : Î² â†’ Î±} {a : Î±} {s : Finset Î²}

@[to_additive]
theorem HasProd.multipliable (h : HasProd f a L) : Multipliable f L :=
  âŸ¨a, hâŸ©

@[to_additive]
theorem tprod_eq_one_of_not_multipliable (h : Â¬Multipliable f L) : âˆ'[L] b, f b = 1 := by
  simp [tprod_def, h]

@[to_additive]
theorem Function.Injective.hasProd_map_iff {L : SummationFilter Î³} {g : Î³ â†’ Î²} (hg : Injective g) :
    HasProd f a (L.map âŸ¨g, hgâŸ©) â†” HasProd (f âˆ˜ g) a L := by
  simp [HasProd, Function.comp_def]

@[to_additive]
theorem Function.Injective.hasProd_comap_iff_of_hasSupport [L.HasSupport] {g : Î³ â†’ Î²}
    (hg : Injective g) (hf : âˆ€ x âˆˆ L.support, x âˆ‰ Set.range g â†’ f x = 1) :
    HasProd (f âˆ˜ g) a (L.comap âŸ¨g, hgâŸ©) â†” HasProd f a L := by
  simp only [HasProd, SummationFilter.comap_filter, tendsto_map'_iff, comp_apply,
    Embedding.coeFn_mk, Function.comp_def]
  refine tendsto_congr' ?_
  filter_upwards [L.eventually_le_support] with s hs
  rw [s.prod_preimage]
  exact fun x h h' â†¦ hf x (hs h) h'

@[to_additive]
theorem Function.Injective.hasProd_comap_iff {g : Î³ â†’ Î²} (hg : Injective g)
    (hf : âˆ€ x, x âˆ‰ Set.range g â†’ f x = 1) :
    HasProd (f âˆ˜ g) a (L.comap âŸ¨g, hgâŸ©) â†” HasProd f a L := by
  simp only [HasProd, SummationFilter.comap_filter, tendsto_map'_iff, comp_apply,
    Embedding.coeFn_mk, Function.comp_def]
  refine tendsto_congr fun s â†¦ ?_
  rw [s.prod_preimage]
  exact fun x _ h â†¦ hf x h

@[to_additive]
theorem Function.Injective.hasProd_iff {g : Î³ â†’ Î²} (hg : Injective g)
    (hf : âˆ€ x, x âˆ‰ Set.range g â†’ f x = 1) :
    HasProd (f âˆ˜ g) a â†” HasProd f a := by
  rw [â† hg.hasProd_comap_iff hf, SummationFilter.comap_unconditional]

@[to_additive]
theorem hasProd_subtype_comap_iff_of_mulSupport_subset {s : Set Î²} (hf : mulSupport f âŠ† s) :
    HasProd (f âˆ˜ (â†‘) : s â†’ Î±) a (L.comap <| Embedding.subtype _) â†” HasProd f a L :=
  Subtype.coe_injective.hasProd_comap_iff <| by simpa using mulSupport_subset_iff'.1 hf

@[to_additive]
theorem hasProd_subtype_iff_of_mulSupport_subset {s : Set Î²} (hf : mulSupport f âŠ† s) :
    HasProd (f âˆ˜ (â†‘) : s â†’ Î±) a â†” HasProd f a :=
  by simpa using hasProd_subtype_comap_iff_of_mulSupport_subset hf (L := unconditional _)

@[to_additive]
theorem hasProd_fintype_support [Fintype Î²] (f : Î² â†’ Î±) (L : SummationFilter Î²) [L.HasSupport]
    [DecidablePred (Â· âˆˆ L.support)] : HasProd f (âˆ b âˆˆ L.support, f b) L := by
  apply tendsto_nhds_of_eventually_eq
  have h1 : â‹‚ b âˆˆ L.support, {s | b âˆˆ s} âˆˆ L.filter :=
    (L.filter.biInter_mem L.support.toFinite).mpr (by tauto)
  have h2 : â‹‚ b âˆˆ L.supportá¶œ, {s | b âˆ‰ s} âˆˆ L.filter :=
    (L.filter.biInter_mem L.supportá¶œ.toFinite).mpr
      (fun b hb â†¦ (L.eventually_mem_or_not_mem b).resolve_left hb)
  filter_upwards [h1, h2] with s hs hs'
  congr 1
  simp only [Set.mem_iInter, Set.mem_setOf_eq, Set.mem_compl_iff] at hs hs'
  grind [Set.mem_toFinset]

@[to_additive]
theorem hasProd_fintype [Fintype Î²] (f : Î² â†’ Î±) (L := unconditional Î²) [L.LeAtTop] :
    HasProd f (âˆ b, f b) L :=
  by simpa using hasProd_fintype_support f L

@[to_additive]
theorem Finset.hasProd_support (s : Finset Î²) (f : Î² â†’ Î±) (L := unconditional (s : Set Î²))
    [L.HasSupport] [DecidablePred (Â· âˆˆ L.support)] :
    HasProd (f âˆ˜ (â†‘) : (â†‘s : Set Î²) â†’ Î±)
      (âˆ b âˆˆ (L.support.toFinset.map <| Embedding.subtype _), f b) L := by
  simpa [prod_attach] using hasProd_fintype_support (f âˆ˜ Subtype.val) L

-- note this is not deduced from `Finset.hasProd_support` to avoid needing `[DecidableEq Î²]`
@[to_additive]
protected theorem Finset.hasProd (s : Finset Î²) (f : Î² â†’ Î±)
    (L := unconditional (s : Set Î²)) [L.LeAtTop] :
    HasProd (f âˆ˜ (â†‘) : (â†‘s : Set Î²) â†’ Î±) (âˆ b âˆˆ s, f b) L := by
  simpa [prod_attach, Embedding.subtype] using Finset.hasProd_support s f L

/-- If a function `f` is `1` outside of a finite set `s`, then it `HasProd` `âˆ b âˆˆ s, f b`. -/
@[to_additive /-- If a function `f` vanishes outside of a finite set `s`, then it `HasSum`
`âˆ‘ b âˆˆ s, f b`. -/]
theorem hasProd_prod_support_of_ne_finset_one (hf : âˆ€ b âˆˆ L.support, b âˆ‰ s â†’ f b = 1)
    [L.HasSupport] [DecidablePred (Â· âˆˆ L.support)] :
    HasProd f (âˆ b âˆˆ (â†‘s âˆ© L.support).toFinset, f b) L := by
  apply tendsto_nhds_of_eventually_eq
  have h1 : â‹‚ b âˆˆ (â†‘s âˆ© L.support), {s | b âˆˆ s} âˆˆ L.filter :=
    (L.filter.biInter_mem (Set.toFinite _)).mpr (fun b hb â†¦ hb.2)
  filter_upwards [h1, L.eventually_le_support] with t ht ht'
  simp only [Set.mem_iInter] at ht
  apply Finset.prod_congr_of_eq_on_inter <;>
  Â· simp only [Set.mem_toFinset]
    grind

/-- If a function `f` is `1` outside of a finite set `s`, then it `HasProd` `âˆ b âˆˆ s, f b`. -/
@[to_additive /-- If a function `f` vanishes outside of a finite set `s`, then it `HasSum`
`âˆ‘ b âˆˆ s, f b`. -/]
theorem hasProd_prod_of_ne_finset_one (hf : âˆ€ b âˆ‰ s, f b = 1) [L.LeAtTop] :
    HasProd f (âˆ b âˆˆ s, f b) L :=
  ((hasProd_subtype_iff_of_mulSupport_subset <| mulSupport_subset_iff'.2 hf).1 <| s.hasProd f)
    |>.mono_left L.le_atTop

@[to_additive]
theorem multipliable_of_ne_finset_one (hf : âˆ€ b âˆ‰ s, f b = 1) [L.HasSupport] :
    Multipliable f L := by
  classical
  exact (hasProd_prod_support_of_ne_finset_one (fun b _ hb â†¦ hf b hb)).multipliable

@[to_additive]
theorem Multipliable.hasProd (ha : Multipliable f L) : HasProd f (âˆ'[L] b, f b) L := by
  -- This is quite delicate because of the fiddly special-casing for finite products.
  classical
  rw [tprod_def, dif_pos ha]
  split_ifs with h
  Â· convert hasProd_prod_support_of_ne_finset_one (s := h.2.toFinset) (L := L) _ using 2
    Â· simp only [Set.inter_eq_left.mpr (show â†‘h.2.toFinset âŠ† L.support by simp)]
      simp only [Set.Finite.coe_toFinset, Finset.toFinset_coe]
      rw [finprod_eq_prod_of_mulSupport_subset (s := h.2.toFinset)]
      Â· exact Finset.prod_congr rfl (by aesop)
      Â· simp
    Â· grind [Set.Finite.mem_toFinset, mem_mulSupport]
    Â· exact h.1
  Â· exact ha.choose_spec

variable [T2Space Î±] [L.NeBot]

@[to_additive]
theorem HasProd.unique {aâ‚ aâ‚‚ : Î±} :
    HasProd f aâ‚ L â†’ HasProd f aâ‚‚ L â†’ aâ‚ = aâ‚‚ := by
  classical exact tendsto_nhds_unique

@[to_additive]
theorem HasProd.tprod_eq (ha : HasProd f a L) : âˆ'[L] b, f b = a :=
  (Multipliable.hasProd âŸ¨a, haâŸ©).unique ha

@[to_additive]
theorem Multipliable.hasProd_iff (h : Multipliable f L) :
    HasProd f a L â†” âˆ'[L] b, f b = a :=
  Iff.intro HasProd.tprod_eq fun eq â†¦ eq â–¸ h.hasProd

end HasProd
