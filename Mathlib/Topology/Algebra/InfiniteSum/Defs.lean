/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl
-/
import Mathlib.Algebra.BigOperators.Finprod
import Mathlib.Order.Filter.AtTopBot.BigOperators
import Mathlib.Topology.Separation.Hausdorff

/-!
# Infinite sum and product over a topological monoid

This file defines unconditionally convergent sums over a commutative topological additive monoid.
For Euclidean spaces (finite-dimensional Banach spaces) this is equivalent to absolute
convergence.

We also define unconditionally convergent products over a commutative topological multiplicative
monoid.

Note: There are summable sequences which are not unconditionally convergent! The other way holds
generally, see `HasSum.tendsto_sum_nat`.

## Implementation notes

We say that a function `f : Œ≤ ‚Üí Œ±` has an unconditional product of `a` if the function
`fun s : Finset Œ≤ ‚Ü¶ ‚àè b ‚àà s, f b` converges to `a` on the `atTop` filter on `Finset Œ≤`. In other
words, for every neighborhood `U` of `a`, there exists a finite set `s : Finset Œ≤` of indices such
that `‚àè b ‚àà s', f b ‚àà U` for any finite set `s'` which is a superset of `s`.

This may yield some unexpected results. For example, according to this definition, the product
`‚àè' n : ‚Ñï, (1 : ‚Ñù) / 2` unconditionally exists and is equal to `0`. More strikingly,
the product `‚àè' n : ‚Ñï, (n : ‚Ñù)` unconditionally exists and is equal to `0`, because one
of its terms is `0` (even though the product of the remaining terms diverges). Users who would
prefer that these products be considered not to exist can carry them out in the unit group `‚ÑùÀ£`
rather than in `‚Ñù`.

## References

* Bourbaki: General Topology (1995), Chapter 3 ¬ß5 (Infinite sums in commutative groups)

-/

/- **NOTE**. This file is intended to be kept short, just enough to state the basic definitions and
six key lemmas relating them together, namely `Summable.hasSum`, `Multipliable.hasProd`,
`HasSum.tsum_eq`, `HasProd.tprod_eq`, `Summable.hasSum_iff`, and `Multipliable.hasProd_iff`.

Do not add further lemmas here -- add them to `InfiniteSum.Basic` or (preferably) another, more
specific file. -/

noncomputable section

open Filter Function

open scoped Topology

variable {Œ± Œ≤ Œ≥ : Type*}

section HasProd

/-- A filter on the set of finsets of a type. (Used for defining summation methods.) -/
structure SummationFilter (Œ≤) where
  /-- The filter -/
  filter : Filter (Finset Œ≤)
  /-- Proof that the filter is `‚â§ atTop` -/
  le_atTop : filter ‚â§ atTop
  /-- Proof that the filter is `‚â† ‚ä•` -/
  ne_bot : filter.NeBot

attribute [instance] SummationFilter.ne_bot

/-- Unconditional summation: a function on `Œ≤` is said to be summable if its partial
sums over finite subsets converge with respect to the `atTop` filter. -/
def unconditional : SummationFilter Œ≤ := ‚ü®atTop, le_rfl, atTop_neBot‚ü©

instance [Countable Œ≤] : IsCountablyGenerated (unconditional : SummationFilter Œ≤).filter :=
  atTop.isCountablyGenerated

variable [CommMonoid Œ±] [TopologicalSpace Œ±]

/-- `HasProd f a` means that the (potentially infinite) product of the `f b` for `b : Œ≤` converges
to `a`.

The `atTop` filter on `Finset Œ≤` is the limit of all finite sets towards the entire type. So we take
the product over bigger and bigger sets. This product operation is invariant under reordering.

For the definition and many statements, `Œ±` does not need to be a topological monoid. We only add
this assumption later, for the lemmas where it is relevant.

These are defined in an identical way to infinite sums (`HasSum`). For example, we say that
the function `‚Ñï ‚Üí ‚Ñù` sending `n` to `1 / 2` has a product of `0`, rather than saying that it does
not converge as some authors would. -/
@[to_additive /-- `HasSum f a` means that the (potentially infinite) sum of the `f b` for `b : Œ≤`
converges to `a`.

The `atTop` filter on `Finset Œ≤` is the limit of all finite sets towards the entire type. So we sum
up bigger and bigger sets. This sum operation is invariant under reordering. In particular,
the function `‚Ñï ‚Üí ‚Ñù` sending `n` to `(-1)^n / (n+1)` does not have a
sum for this definition, but a series which is absolutely convergent will have the correct sum.

This is based on Mario Carneiro's
[infinite sum `df-tsms` in Metamath](http://us.metamath.org/mpeuni/df-tsms.html).

For the definition and many statements, `Œ±` does not need to be a topological monoid. We only add
this assumption later, for the lemmas where it is relevant. -/]
def HasProd (f : Œ≤ ‚Üí Œ±) (a : Œ±) (L : SummationFilter Œ≤ := unconditional) : Prop :=
  Tendsto (fun s : Finset Œ≤ ‚Ü¶ ‚àè b ‚àà s, f b) L.filter (ùìù a)

/-- `Multipliable f` means that `f` has some (infinite) product. Use `tprod` to get the value. -/
@[to_additive
/-- `Summable f` means that `f` has some (infinite) sum. Use `tsum` to get the value. -/]
def Multipliable (f : Œ≤ ‚Üí Œ±) (L : SummationFilter Œ≤ := unconditional) : Prop :=
  ‚àÉ a, HasProd f a L

@[to_additive]
lemma Multipliable.mono_filter {f : Œ≤ ‚Üí Œ±} {L‚ÇÅ L‚ÇÇ : SummationFilter Œ≤}
    (hf : Multipliable f L‚ÇÇ) (h : L‚ÇÅ.filter ‚â§ L‚ÇÇ.filter) : Multipliable f L‚ÇÅ :=
  match hf with | ‚ü®a, ha‚ü© => ‚ü®a, ha.mono_left h‚ü©

open scoped Classical in
/-- `‚àè' i, f i` is the product of `f` if it exists and is unconditionally convergent,
or 1 otherwise. -/
@[to_additive /-- `‚àë' i, f i` is the sum of `f` if it exists and is unconditionally convergent,
or 0 otherwise. -/]
noncomputable irreducible_def tprod (f : Œ≤ ‚Üí Œ±) (L : SummationFilter Œ≤ := unconditional) :=
  if h : Multipliable f L then
  /- Note that the product might not be uniquely defined if the topology is not separated.
  When the multiplicative support of `f` is finite, we make the most reasonable choice to use the
  product over the multiplicative support. Otherwise, we choose arbitrarily an `a` satisfying
  `HasProd f a`. -/
    if (mulSupport f).Finite then finprod f
    else h.choose
  else 1

variable {L : SummationFilter Œ≤}

@[inherit_doc tprod]
notation3 "‚àè'[" L "]" (...)", "r:67:(scoped f => tprod f L) => r
@[inherit_doc tsum]
notation3 "‚àë'[" L "]" (...)", "r:67:(scoped f => tsum f L) => r

-- see Note [operator precedence of big operators]
@[inherit_doc tprod]
notation3 "‚àè' "(...)", "r:67:(scoped f => tprod f) => r
@[inherit_doc tsum]
notation3 "‚àë' "(...)", "r:67:(scoped f => tsum f) => r

variable {f : Œ≤ ‚Üí Œ±} {a : Œ±} {s : Finset Œ≤}

@[to_additive]
theorem HasProd.multipliable (h : HasProd f a L) : Multipliable f L :=
  ‚ü®a, h‚ü©

@[to_additive]
theorem tprod_eq_one_of_not_multipliable (h : ¬¨Multipliable f L) : ‚àè'[L] b, f b = 1 := by
  simp [tprod_def, h]

-- didn't find a way to "filterize" this one
@[to_additive]
theorem Function.Injective.hasProd_iff {g : Œ≥ ‚Üí Œ≤} (hg : Injective g)
    (hf : ‚àÄ x, x ‚àâ Set.range g ‚Üí f x = 1) : HasProd (f ‚àò g) a ‚Üî HasProd f a := by
  simp only [HasProd, Tendsto, comp_apply, unconditional, hg.map_atTop_finset_prod_eq hf]

-- didn't find a way to "filterize" this one
@[to_additive]
theorem hasProd_subtype_iff_of_mulSupport_subset {s : Set Œ≤} (hf : mulSupport f ‚äÜ s) :
    HasProd (f ‚àò (‚Üë) : s ‚Üí Œ±) a ‚Üî HasProd f a :=
  Subtype.coe_injective.hasProd_iff <| by simpa using mulSupport_subset_iff'.1 hf

@[to_additive]
theorem hasProd_fintype [Fintype Œ≤] (f : Œ≤ ‚Üí Œ±) :
    HasProd f (‚àè b, f b) L :=
  (OrderTop.tendsto_atTop_nhds _).mono_left L.le_atTop

@[to_additive]
protected theorem Finset.hasProd (s : Finset Œ≤) (f : Œ≤ ‚Üí Œ±) {L : SummationFilter (s : Set Œ≤)} :
    HasProd (f ‚àò (‚Üë) : (‚Üës : Set Œ≤) ‚Üí Œ±) (‚àè b ‚àà s, f b) L := by
  rw [‚Üê prod_attach]
  exact hasProd_fintype _

/-- If a function `f` is `1` outside of a finite set `s`, then it `HasProd` `‚àè b ‚àà s, f b`. -/
@[to_additive /-- If a function `f` vanishes outside of a finite set `s`, then it `HasSum`
`‚àë b ‚àà s, f b`. -/]
theorem hasProd_prod_of_ne_finset_one (hf : ‚àÄ b ‚àâ s, f b = 1) :
    HasProd f (‚àè b ‚àà s, f b) L :=
  ((hasProd_subtype_iff_of_mulSupport_subset <| mulSupport_subset_iff'.2 hf).1 <| s.hasProd f)
    |>.mono_left L.le_atTop

@[to_additive]
theorem multipliable_of_ne_finset_one (hf : ‚àÄ b ‚àâ s, f b = 1) :
    Multipliable f L :=
  (hasProd_prod_of_ne_finset_one hf).multipliable

@[to_additive]
theorem Multipliable.hasProd (ha : Multipliable f L) : HasProd f (‚àè'[L] b, f b) L := by
  simp only [tprod_def, ha, dite_true]
  split_ifs with h
  ¬∑ simpa [h, finprod_eq_prod] using (hasProd_prod_of_ne_finset_one (by simp))
  ¬∑ exact ha.choose_spec

variable [T2Space Œ±]

@[to_additive]
theorem HasProd.unique {a‚ÇÅ a‚ÇÇ : Œ±} :
    HasProd f a‚ÇÅ L ‚Üí HasProd f a‚ÇÇ L ‚Üí a‚ÇÅ = a‚ÇÇ := by
  classical exact tendsto_nhds_unique

@[to_additive]
theorem HasProd.tprod_eq (ha : HasProd f a L) : ‚àè'[L] b, f b = a :=
  (Multipliable.hasProd ‚ü®a, ha‚ü©).unique ha

@[to_additive]
theorem Multipliable.hasProd_iff (h : Multipliable f L) : HasProd f a L ‚Üî ‚àè'[L] b, f b = a :=
  Iff.intro HasProd.tprod_eq fun eq ‚Ü¶ eq ‚ñ∏ h.hasProd

end HasProd
