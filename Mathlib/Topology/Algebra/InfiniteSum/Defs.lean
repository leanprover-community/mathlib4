/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl
-/
import Mathlib.Topology.Separation
import Mathlib.Algebra.BigOperators.Finprod

#align_import topology.algebra.infinite_sum.basic from "leanprover-community/mathlib"@"3b52265189f3fb43aa631edffce5d060fafaf82f"

/-!
# Infinite sum and product over a topological monoid

This file defines unconditionally convergent sums over a commutative topological additive monoid.
For Euclidean spaces (finite dimensional Banach spaces) this is equivalent to absolute
convergence.

We also define unconditionally convergent products over a commutative topological multiplicative
monoid.

Note: There are summable sequences which are not unconditionally convergent! The other way holds
generally, see `HasSum.tendsto_sum_nat`.

## Implementation notes

We say that a function `f : Î² â†’ Î±` has an unconditional product of `a` if the function
`fun s : Finset Î² â†¦ âˆ b âˆˆ s, f b` converges to `a` on the `atTop` filter on `Finset Î²`. In other
words, for every neighborhood `U` of `a`, there exists a finite set `s : Finset Î²` of indices such
that `âˆ b âˆˆ s', f b âˆˆ U` for any finite set `s'` which is a superset of `s`.

This may yield some unexpected results. For example, according to this definition, the product
`âˆ' n : â„•, (1 : â„) / 2` unconditionally exists and is equal to `0`. More strikingly,
the product `âˆ' n : â„•, (n : â„)` unconditionally exists and is equal to `0`, because one
of its terms is `0` (even though the product of the remaining terms diverges). Users who would
prefer that these products be considered not to exist can carry them out in the unit group `â„Ë£`
rather than in `â„`.

## References

* Bourbaki: General Topology (1995), Chapter 3 Â§5 (Infinite sums in commutative groups)

-/

/- **NOTE**. This file is intended to be kept short, just enough to state the basic definitions and
six key lemmas relating them together, namely `Summable.hasSum`, `Multipliable.hasProd`,
`HasSum.tsum_eq`, `HasProd.tprod_eq`, `Summable.hasSum_iff`, and `Multipliable.hasProd_iff`.

Do not add further lemmas here -- add them to `InfiniteSum.Basic` or (preferably) another, more
specific file. -/

noncomputable section

open Filter Function

open scoped Topology

variable {Î± Î² Î³ : Type*}

section HasProd

variable [CommMonoid Î±] [TopologicalSpace Î±]

/-- Infinite product on a topological monoid

The `atTop` filter on `Finset Î²` is the limit of all finite sets towards the entire type. So we take
the product over bigger and bigger sets. This product operation is invariant under reordering.

For the definition and many statements, `Î±` does not need to be a topological monoid. We only add
this assumption later, for the lemmas where it is relevant.

These are defined in an identical way to infinite sums (`HasSum`). For example, we say that
the function `â„• â†’ â„` sending `n` to `1 / 2` has a product of `0`, rather than saying that it does
not converge as some authors would. -/
@[to_additive "Infinite sum on a topological monoid

The `atTop` filter on `Finset Î²` is the limit of all finite sets towards the entire type. So we sum
up bigger and bigger sets. This sum operation is invariant under reordering. In particular,
the function `â„• â†’ â„` sending `n` to `(-1)^n / (n+1)` does not have a
sum for this definition, but a series which is absolutely convergent will have the correct sum.

This is based on Mario Carneiro's
[infinite sum `df-tsms` in Metamath](http://us.metamath.org/mpeuni/df-tsms.html).

For the definition and many statements, `Î±` does not need to be a topological monoid. We only add
this assumption later, for the lemmas where it is relevant."]
def HasProd (f : Î² â†’ Î±) (a : Î±) : Prop :=
  Tendsto (fun s : Finset Î² â†¦ âˆ b âˆˆ s, f b) atTop (ğ“ a)
#align has_sum HasSum

/-- `Multipliable f` means that `f` has some (infinite) product. Use `tprod` to get the value. -/
@[to_additive "`Summable f` means that `f` has some (infinite) sum. Use `tsum` to get the value."]
def Multipliable (f : Î² â†’ Î±) : Prop :=
  âˆƒ a, HasProd f a
#align summable Summable

open scoped Classical in
/-- `âˆ' i, f i` is the product of `f` it exists, or 1 otherwise. -/
@[to_additive "`âˆ‘' i, f i` is the sum of `f` it exists, or 0 otherwise."]
noncomputable irreducible_def tprod {Î²} (f : Î² â†’ Î±) :=
  if h : Multipliable f then
  /- Note that the product might not be uniquely defined if the topology is not separated.
  When the multiplicative support of `f` is finite, we make the most reasonable choice to use the
  product over the multiplicative support. Otherwise, we choose arbitrarily an `a` satisfying
  `HasProd f a`. -/
    if (mulSupport f).Finite then finprod f
    else h.choose
  else 1
#align tsum tsum

-- see Note [operator precedence of big operators]
@[inherit_doc tprod]
notation3 "âˆ' "(...)", "r:67:(scoped f => tprod f) => r
@[inherit_doc tsum]
notation3 "âˆ‘' "(...)", "r:67:(scoped f => tsum f) => r

variable {f g : Î² â†’ Î±} {a b : Î±} {s : Finset Î²}

@[to_additive]
theorem HasProd.multipliable (h : HasProd f a) : Multipliable f :=
  âŸ¨a, hâŸ©
#align has_sum.summable HasSum.summable

@[to_additive]
theorem tprod_eq_one_of_not_multipliable (h : Â¬Multipliable f) : âˆ' b, f b = 1 := by
  simp [tprod_def, h]
#align tsum_eq_zero_of_not_summable tsum_eq_zero_of_not_summable

@[to_additive]
theorem Function.Injective.hasProd_iff {g : Î³ â†’ Î²} (hg : Injective g)
    (hf : âˆ€ x, x âˆ‰ Set.range g â†’ f x = 1) : HasProd (f âˆ˜ g) a â†” HasProd f a := by
  simp only [HasProd, Tendsto, comp_apply, hg.map_atTop_finset_prod_eq hf]
#align function.injective.has_sum_iff Function.Injective.hasSum_iff

@[to_additive]
theorem hasProd_subtype_iff_of_mulSupport_subset {s : Set Î²} (hf : mulSupport f âŠ† s) :
    HasProd (f âˆ˜ (â†‘) : s â†’ Î±) a â†” HasProd f a :=
  Subtype.coe_injective.hasProd_iff <| by simpa using mulSupport_subset_iff'.1 hf
#align has_sum_subtype_iff_of_support_subset hasSum_subtype_iff_of_support_subset

@[to_additive]
theorem hasProd_fintype [Fintype Î²] (f : Î² â†’ Î±) : HasProd f (âˆ b, f b) :=
  OrderTop.tendsto_atTop_nhds _
#align has_sum_fintype hasSum_fintype

@[to_additive]
protected theorem Finset.hasProd (s : Finset Î²) (f : Î² â†’ Î±) :
    HasProd (f âˆ˜ (â†‘) : (â†‘s : Set Î²) â†’ Î±) (âˆ b âˆˆ s, f b) := by
  rw [â† prod_attach]
  exact hasProd_fintype _
#align finset.has_sum Finset.hasSum

/-- If a function `f` is `1` outside of a finite set `s`, then it `HasProd` `âˆ b âˆˆ s, f b`. -/
@[to_additive "If a function `f` vanishes outside of a finite set `s`, then it `HasSum`
`âˆ‘ b âˆˆ s, f b`."]
theorem hasProd_prod_of_ne_finset_one (hf : âˆ€ b âˆ‰ s, f b = 1) :
    HasProd f (âˆ b âˆˆ s, f b) :=
  (hasProd_subtype_iff_of_mulSupport_subset <| mulSupport_subset_iff'.2 hf).1 <| s.hasProd f
#align has_sum_sum_of_ne_finset_zero hasSum_sum_of_ne_finset_zero

@[to_additive]
theorem multipliable_of_ne_finset_one (hf : âˆ€ b âˆ‰ s, f b = 1) : Multipliable f :=
  (hasProd_prod_of_ne_finset_one hf).multipliable
#align summable_of_ne_finset_zero summable_of_ne_finset_zero

@[to_additive]
theorem Multipliable.hasProd (ha : Multipliable f) : HasProd f (âˆ' b, f b) := by
  simp only [tprod_def, ha, dite_true]
  by_cases H : (mulSupport f).Finite
  Â· simp [H, hasProd_prod_of_ne_finset_one, finprod_eq_prod]
  Â· simpa [H] using ha.choose_spec
#align summable.has_sum Summable.hasSum

@[to_additive]
theorem HasProd.unique {aâ‚ aâ‚‚ : Î±} [T2Space Î±] : HasProd f aâ‚ â†’ HasProd f aâ‚‚ â†’ aâ‚ = aâ‚‚ := by
  classical exact tendsto_nhds_unique
#align has_sum.unique HasSum.unique

variable [T2Space Î±]

@[to_additive]
theorem HasProd.tprod_eq (ha : HasProd f a) : âˆ' b, f b = a :=
  (Multipliable.hasProd âŸ¨a, haâŸ©).unique ha
#align has_sum.tsum_eq HasSum.tsum_eq

@[to_additive]
theorem Multipliable.hasProd_iff (h : Multipliable f) : HasProd f a â†” âˆ' b, f b = a :=
  Iff.intro HasProd.tprod_eq fun eq â†¦ eq â–¸ h.hasProd
#align summable.has_sum_iff Summable.hasSum_iff

end HasProd
