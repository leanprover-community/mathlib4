/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl
-/
import Mathlib.Topology.Separation
import Mathlib.Algebra.BigOperators.Finprod

#align_import topology.algebra.infinite_sum.basic from "leanprover-community/mathlib"@"3b52265189f3fb43aa631edffce5d060fafaf82f"

/-!
# Infinite sum and product over a topological monoid

This file defines unconditionally convergent sums over a commutative topological additive monoid.
For Euclidean spaces (finite dimensional Banach spaces) this is equivalent to absolute
convergence.

We also define unconditionally convergent products over a commutative topological multiplicative
monoid.

Note: There are summable sequences which are not unconditionally convergent! The other way holds
generally, see `HasSum.tendsto_sum_nat`.

## References

* Bourbaki: General Topology (1995), Chapter 3 ¬ß5 (Infinite sums in commutative groups)

-/

/- **NOTE**. This file is intended to be kept short, just enough to state the basic definitions and
three key lemmas relating them together, namely `Summable.hasSum`, `Prodable.hasProd`,
`HasSum.tsum_eq`, `HasProd.tprod_eq`, `Summable.hasSum_iff`, and `Prodable.hasProd_iff`.

Do not add further lemmas here -- add them to `InfiniteSum.Basic` or (preferably) another, more
specific file. -/

noncomputable section

open Filter Function

open scoped BigOperators Topology

variable {Œ± Œ≤ Œ≥ : Type*}

section HasProd

variable [CommMonoid Œ±] [TopologicalSpace Œ±]

/-- Infinite product on a topological monoid

These are defined in an identical way to infinite sums (`HasSum`). For example, we say that
the function `‚Ñï ‚Üí ‚Ñù` sending `n` to `1 / 2` has a product of `0`, rather than saying that it does
not converge as some authors would.
-/
@[to_additive "Infinite sum on a topological monoid

The `atTop` filter on `Finset Œ≤` is the limit of all finite sets towards the entire type. So we sum
up bigger and bigger sets. This sum operation is invariant under reordering. In particular,
the function `‚Ñï ‚Üí ‚Ñù` sending `n` to `(-1)^n / (n+1)` does not have a
sum for this definition, but a series which is absolutely convergent will have the correct sum.

This is based on Mario Carneiro's
[infinite sum `df-tsms` in Metamath](http://us.metamath.org/mpeuni/df-tsms.html).

For the definition or many statements, `Œ±` does not need to be a topological monoid. We only add
this assumption later, for the lemmas where it is relevant."]
def HasProd (f : Œ≤ ‚Üí Œ±) (a : Œ±) : Prop :=
  Tendsto (fun s : Finset Œ≤ ‚Ü¶ ‚àè b in s, f b) atTop (ùìù a)
#align has_sum HasSum

/-- `Prodable f` means that `f` has some (infinite) product. Use `tprod` to get the value. -/
@[to_additive "`Summable f` means that `f` has some (infinite) sum. Use `tsum` to get the value."]
def Prodable (f : Œ≤ ‚Üí Œ±) : Prop :=
  ‚àÉ a, HasProd f a
#align summable Summable

open scoped Classical in
/-- `‚àè' i, f i` is the product of `f` it exists, or 1 otherwise. -/
@[to_additive "`‚àë' i, f i` is the sum of `f` it exists, or 0 otherwise."]
noncomputable irreducible_def tprod {Œ≤} (f : Œ≤ ‚Üí Œ±) :=
  if h : Prodable f then
  /- Note that the sum might not be uniquely defined if the topology is not separated.
  When the support of `f` is finite, we make the most reasonable choice to use the finite sum over
  the support. Otherwise, we choose arbitrarily an `a` satisfying `HasSum f a`. -/
    if (mulSupport f).Finite then finprod f
    else h.choose
  else 1
#align tsum tsum

-- see Note [operator precedence of big operators]
@[inherit_doc tprod]
notation3 "‚àè' "(...)", "r:67:(scoped f => tprod f) => r
@[inherit_doc tsum]
notation3 "‚àë' "(...)", "r:67:(scoped f => tsum f) => r

variable {f g : Œ≤ ‚Üí Œ±} {a b : Œ±} {s : Finset Œ≤}

@[to_additive]
theorem HasProd.prodable (h : HasProd f a) : Prodable f :=
  ‚ü®a, h‚ü©
#align has_sum.summable HasSum.summable

@[to_additive]
theorem tprod_eq_one_of_not_prodable (h : ¬¨Prodable f) : ‚àè' b, f b = 1 := by simp [tprod_def, h]
#align tsum_eq_zero_of_not_summable tsum_eq_zero_of_not_summable

@[to_additive]
theorem Function.Injective.hasProd_iff {g : Œ≥ ‚Üí Œ≤} (hg : Injective g)
    (hf : ‚àÄ x, x ‚àâ Set.range g ‚Üí f x = 1) : HasProd (f ‚àò g) a ‚Üî HasProd f a := by
  simp only [HasProd, Tendsto, comp_apply, hg.map_atTop_finset_prod_eq hf]
#align function.injective.has_sum_iff Function.Injective.hasSum_iff

@[to_additive]
theorem hasProd_subtype_iff_of_mulSupport_subset {s : Set Œ≤} (hf : mulSupport f ‚äÜ s) :
    HasProd (f ‚àò (‚Üë) : s ‚Üí Œ±) a ‚Üî HasProd f a :=
  Subtype.coe_injective.hasProd_iff <| by simpa using mulSupport_subset_iff'.1 hf
#align has_sum_subtype_iff_of_support_subset hasSum_subtype_iff_of_support_subset

@[to_additive]
theorem hasProd_fintype [Fintype Œ≤] (f : Œ≤ ‚Üí Œ±) : HasProd f (‚àè b, f b) :=
  OrderTop.tendsto_atTop_nhds _
#align has_sum_fintype hasSum_fintype

@[to_additive]
protected theorem Finset.hasProd (s : Finset Œ≤) (f : Œ≤ ‚Üí Œ±) :
    HasProd (f ‚àò (‚Üë) : (‚Üës : Set Œ≤) ‚Üí Œ±) (‚àè b in s, f b) := by
  rw [‚Üê prod_attach]
  exact hasProd_fintype _
#align finset.has_sum Finset.hasSum

/-- If a function `f` is `1` outside of a finite set `s`, then it `HasProd` `‚àè b in s, f b`. -/
@[to_additive "If a function `f` vanishes outside of a finite set `s`, then it `HasSum`
`‚àë b in s, f b`."]
theorem hasProd_prod_of_ne_finset_one (hf : ‚àÄ (b) (_ : b ‚àâ s), f b = 1) :
    HasProd f (‚àè b in s, f b) :=
  (hasProd_subtype_iff_of_mulSupport_subset <| mulSupport_subset_iff'.2 hf).1 <| s.hasProd f
#align has_sum_sum_of_ne_finset_zero hasSum_sum_of_ne_finset_zero

@[to_additive]
theorem prodable_of_ne_finset_one (hf : ‚àÄ (b) (_ : b ‚àâ s), f b = 1) : Prodable f :=
  (hasProd_prod_of_ne_finset_one hf).prodable
#align summable_of_ne_finset_zero summable_of_ne_finset_zero

@[to_additive]
theorem Prodable.hasProd (ha : Prodable f) : HasProd f (‚àè' b, f b) := by
  simp only [tprod_def, ha, dite_true]
  by_cases H : (mulSupport f).Finite
  ¬∑ simp [H, hasProd_prod_of_ne_finset_one, finprod_eq_prod]
  ¬∑ simpa [H] using ha.choose_spec
#align summable.has_sum Summable.hasSum

@[to_additive]
theorem HasProd.unique {a‚ÇÅ a‚ÇÇ : Œ±} [T2Space Œ±] : HasProd f a‚ÇÅ ‚Üí HasProd f a‚ÇÇ ‚Üí a‚ÇÅ = a‚ÇÇ := by
  classical exact tendsto_nhds_unique
#align has_sum.unique HasSum.unique

variable [T2Space Œ±]

@[to_additive]
theorem HasProd.tprod_eq (ha : HasProd f a) : ‚àè' b, f b = a :=
  (Prodable.hasProd ‚ü®a, ha‚ü©).unique ha
#align has_sum.tsum_eq HasSum.tsum_eq

@[to_additive]
theorem Prodable.hasProd_iff (h : Prodable f) : HasProd f a ‚Üî ‚àè' b, f b = a :=
  Iff.intro HasProd.tprod_eq fun eq ‚Ü¶ eq ‚ñ∏ h.hasProd
#align summable.has_sum_iff Summable.hasSum_iff

end HasProd
