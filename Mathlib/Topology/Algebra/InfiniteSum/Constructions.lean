/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl
-/
import Mathlib.Order.Filter.AtTopBot.Finset
import Mathlib.Topology.Algebra.InfiniteSum.Group
import Mathlib.Topology.Algebra.Star

/-!
# Topological sums and functorial constructions

Lemmas on the interaction of `tprod`, `tsum`, `HasProd`, `HasSum` etc. with products, Sigma and Pi
types, `MulOpposite`, etc.

-/

noncomputable section

open Filter Finset Function

open scoped Topology

variable {Œ± Œ≤ Œ≥ : Type*}


/-! ## Product, Sigma and Pi types -/

section ProdDomain

variable [CommMonoid Œ±] [TopologicalSpace Œ±]

@[to_additive]
theorem hasProd_pi_single [DecidableEq Œ≤] (b : Œ≤) (a : Œ±) : HasProd (Pi.mulSingle b a) a := by
  convert hasProd_ite_eq b a
  simp [Pi.mulSingle_apply]

@[to_additive (attr := simp)]
theorem tprod_pi_single [DecidableEq Œ≤] (b : Œ≤) (a : Œ±) : ‚àè' b', Pi.mulSingle b a b' = a := by
  rw [tprod_eq_mulSingle b]
  ¬∑ simp
  ¬∑ intro b' hb'; simp [hb']

@[to_additive tsum_setProd_singleton_left]
lemma tprod_setProd_singleton_left (b : Œ≤) (t : Set Œ≥) (f : Œ≤ √ó Œ≥ ‚Üí Œ±) :
    (‚àè' x : {b} √óÀ¢ t, f x) = ‚àè' c : t, f (b, c) := by
  rw [tprod_congr_set_coe _ Set.singleton_prod, tprod_image _ (Prod.mk_right_injective b).injOn]

@[to_additive tsum_setProd_singleton_right]
lemma tprod_setProd_singleton_right (s : Set Œ≤) (c : Œ≥) (f : Œ≤ √ó Œ≥ ‚Üí Œ±) :
    (‚àè' x : s √óÀ¢ {c}, f x) = ‚àè' b : s, f (b, c) := by
  rw [tprod_congr_set_coe _ Set.prod_singleton, tprod_image _ (Prod.mk_left_injective c).injOn]

@[to_additive Summable.prod_symm]
theorem Multipliable.prod_symm {f : Œ≤ √ó Œ≥ ‚Üí Œ±} (hf : Multipliable f) :
    Multipliable fun p : Œ≥ √ó Œ≤ ‚Ü¶ f p.swap :=
  (Equiv.prodComm Œ≥ Œ≤).multipliable_iff.2 hf

end ProdDomain

section ProdCodomain

variable [CommMonoid Œ±] [TopologicalSpace Œ±] [CommMonoid Œ≥] [TopologicalSpace Œ≥]

@[to_additive HasSum.prodMk]
theorem HasProd.prodMk {f : Œ≤ ‚Üí Œ±} {g : Œ≤ ‚Üí Œ≥} {a : Œ±} {b : Œ≥} (hf : HasProd f a)
    (hg : HasProd g b) : HasProd (fun x ‚Ü¶ (‚ü®f x, g x‚ü© : Œ± √ó Œ≥)) ‚ü®a, b‚ü© := by
  simp [HasProd, ‚Üê prod_mk_prod, Filter.Tendsto.prodMk_nhds hf hg]

@[deprecated (since := "2025-03-10")]
alias HasSum.prod_mk := HasSum.prodMk

@[to_additive existing HasSum.prodMk, deprecated (since := "2025-03-10")]
alias HasProd.prod_mk := HasProd.prodMk

end ProdCodomain

section ContinuousMul

variable [CommMonoid Œ±] [TopologicalSpace Œ±] [ContinuousMul Œ±]

section Sum

@[to_additive]
lemma HasProd.sum {Œ± Œ≤ M : Type*} [CommMonoid M] [TopologicalSpace M] [ContinuousMul M]
    {f : Œ± ‚äï Œ≤ ‚Üí M} {a b : M}
    (h‚ÇÅ : HasProd (f ‚àò Sum.inl) a) (h‚ÇÇ : HasProd (f ‚àò Sum.inr) b) : HasProd f (a * b) := by
  have : Tendsto ((‚àè b ‚àà ¬∑, f b) ‚àò sumEquiv.symm) (atTop.map sumEquiv) (nhds (a * b)) := by
    rw [Finset.sumEquiv.map_atTop, ‚Üê prod_atTop_atTop_eq]
    convert (tendsto_mul.comp (nhds_prod_eq (x := a) (y := b) ‚ñ∏ Tendsto.prodMap h‚ÇÅ h‚ÇÇ))
    ext s
    simp
  simpa [Tendsto, ‚Üê Filter.map_map] using this

@[to_additive /-- For the statement that `tsum` commutes with `Finset.sum`,
  see `Summable.tsum_finsetSum`. -/]
protected lemma Multipliable.tprod_sum {Œ± Œ≤ M : Type*} [CommMonoid M] [TopologicalSpace M]
    [ContinuousMul M] [T2Space M] {f : Œ± ‚äï Œ≤ ‚Üí M} (h‚ÇÅ : Multipliable (f ‚àò .inl))
    (h‚ÇÇ : Multipliable (f ‚àò .inr)) : ‚àè' i, f i = (‚àè' i, f (.inl i)) * (‚àè' i, f (.inr i)) :=
  (h‚ÇÅ.hasProd.sum h‚ÇÇ.hasProd).tprod_eq

@[deprecated (since := "2025-04-12")] alias tsum_sum := Summable.tsum_sum
@[to_additive existing, deprecated (since := "2025-04-12")] alias tprod_sum :=
  Multipliable.tprod_sum

@[to_additive]
lemma Multipliable.sum {Œ± Œ≤ M : Type*} [CommMonoid M] [TopologicalSpace M] [ContinuousMul M]
    (f : Œ± ‚äï Œ≤ ‚Üí M) (h‚ÇÅ : Multipliable (f ‚àò Sum.inl)) (h‚ÇÇ : Multipliable (f ‚àò Sum.inr)) :
    Multipliable f :=
  ‚ü®_, .sum h‚ÇÅ.hasProd h‚ÇÇ.hasProd‚ü©

end Sum

section RegularSpace

variable [RegularSpace Œ±]

@[to_additive]
theorem HasProd.sigma {Œ≥ : Œ≤ ‚Üí Type*} {f : (Œ£ b : Œ≤, Œ≥ b) ‚Üí Œ±} {g : Œ≤ ‚Üí Œ±} {a : Œ±}
    (ha : HasProd f a) (hf : ‚àÄ b, HasProd (fun c ‚Ü¶ f ‚ü®b, c‚ü©) (g b)) : HasProd g a := by
  classical
  refine (atTop_basis.tendsto_iff (closed_nhds_basis a)).mpr ?_
  rintro s ‚ü®hs, hsc‚ü©
  rcases mem_atTop_sets.mp (ha hs) with ‚ü®u, hu‚ü©
  use u.image Sigma.fst, trivial
  intro bs hbs
  simp only [Set.mem_preimage, Finset.le_iff_subset] at hu
  have : Tendsto (fun t : Finset (Œ£ b, Œ≥ b) ‚Ü¶ ‚àè p ‚àà t with p.1 ‚àà bs, f p) atTop
      (ùìù <| ‚àè b ‚àà bs, g b) := by
    simp only [‚Üê sigma_preimage_mk, prod_sigma]
    refine tendsto_finset_prod _ fun b _ ‚Ü¶ ?_
    change
      Tendsto (fun t ‚Ü¶ (fun t ‚Ü¶ ‚àè s ‚àà t, f ‚ü®b, s‚ü©) (preimage t (Sigma.mk b) _)) atTop (ùìù (g b))
    exact (hf b).comp (tendsto_finset_preimage_atTop_atTop (sigma_mk_injective))
  refine hsc.mem_of_tendsto this (eventually_atTop.2 ‚ü®u, fun t ht ‚Ü¶ hu _ fun x hx ‚Ü¶ ?_‚ü©)
  exact mem_filter.2 ‚ü®ht hx, hbs <| mem_image_of_mem _ hx‚ü©

/-- If a function `f` on `Œ≤ √ó Œ≥` has product `a` and for each `b` the restriction of `f` to
`{b} √ó Œ≥` has product `g b`, then the function `g` has product `a`. -/
@[to_additive HasSum.prod_fiberwise /-- If a series `f` on `Œ≤ √ó Œ≥` has sum `a` and for each `b` the
restriction of `f` to `{b} √ó Œ≥` has sum `g b`, then the series `g` has sum `a`. -/]
theorem HasProd.prod_fiberwise {f : Œ≤ √ó Œ≥ ‚Üí Œ±} {g : Œ≤ ‚Üí Œ±} {a : Œ±} (ha : HasProd f a)
    (hf : ‚àÄ b, HasProd (fun c ‚Ü¶ f (b, c)) (g b)) : HasProd g a :=
  HasProd.sigma ((Equiv.sigmaEquivProd Œ≤ Œ≥).hasProd_iff.2 ha) hf

@[to_additive]
theorem Multipliable.sigma' {Œ≥ : Œ≤ ‚Üí Type*} {f : (Œ£ b : Œ≤, Œ≥ b) ‚Üí Œ±} (ha : Multipliable f)
    (hf : ‚àÄ b, Multipliable fun c ‚Ü¶ f ‚ü®b, c‚ü©) : Multipliable fun b ‚Ü¶ ‚àè' c, f ‚ü®b, c‚ü© :=
  (ha.hasProd.sigma fun b ‚Ü¶ (hf b).hasProd).multipliable

end RegularSpace

section T3Space

variable [T3Space Œ±]

@[to_additive]
theorem HasProd.sigma_of_hasProd {Œ≥ : Œ≤ ‚Üí Type*} {f : (Œ£ b : Œ≤, Œ≥ b) ‚Üí Œ±} {g : Œ≤ ‚Üí Œ±}
    {a : Œ±} (ha : HasProd g a) (hf : ‚àÄ b, HasProd (fun c ‚Ü¶ f ‚ü®b, c‚ü©) (g b)) (hf' : Multipliable f) :
    HasProd f a := by simpa [(hf'.hasProd.sigma hf).unique ha] using hf'.hasProd

@[to_additive]
protected theorem Multipliable.tprod_sigma' {Œ≥ : Œ≤ ‚Üí Type*} {f : (Œ£ b : Œ≤, Œ≥ b) ‚Üí Œ±}
    (h‚ÇÅ : ‚àÄ b, Multipliable fun c ‚Ü¶ f ‚ü®b, c‚ü©) (h‚ÇÇ : Multipliable f) :
    ‚àè' p, f p = ‚àè' (b) (c), f ‚ü®b, c‚ü© :=
  (h‚ÇÇ.hasProd.sigma fun b ‚Ü¶ (h‚ÇÅ b).hasProd).tprod_eq.symm

@[deprecated (since := "2025-04-12")] alias tsum_sigma' := Summable.tsum_sigma'
@[to_additive existing, deprecated (since := "2025-04-12")] alias tprod_sigma' :=
  Multipliable.tprod_sigma'

@[to_additive Summable.tsum_prod']
protected theorem Multipliable.tprod_prod' {f : Œ≤ √ó Œ≥ ‚Üí Œ±} (h : Multipliable f)
    (h‚ÇÅ : ‚àÄ b, Multipliable fun c ‚Ü¶ f (b, c)) :
    ‚àè' p, f p = ‚àè' (b) (c), f (b, c) :=
  (h.hasProd.prod_fiberwise fun b ‚Ü¶ (h‚ÇÅ b).hasProd).tprod_eq.symm

@[deprecated (since := "2025-04-12")] alias tsum_prod' := Summable.tsum_prod'
@[to_additive existing Summable.tsum_prod', deprecated (since := "2025-04-12")] alias tprod_prod' :=
  Multipliable.tprod_prod'

@[to_additive Summable.tsum_prod_uncurry]
protected theorem Multipliable.tprod_prod_uncurry {f : Œ≤ ‚Üí Œ≥ ‚Üí Œ±}
    (h : Multipliable (Function.uncurry f)) (h‚ÇÅ : ‚àÄ b, Multipliable fun c ‚Ü¶ f b c) :
    ‚àè' p : Œ≤ √ó Œ≥, uncurry f p = ‚àè' (b) (c), f b c :=
  (h.hasProd.prod_fiberwise fun b ‚Ü¶ (h‚ÇÅ b).hasProd).tprod_eq.symm

@[deprecated (since := "2025-04-12")] alias tsum_prod_uncurry :=
  Summable.tsum_prod_uncurry
@[to_additive existing Summable.tsum_prod_uncurry, deprecated (since := "2025-04-12")] alias
  tprod_prod_uncurry := Multipliable.tprod_prod_uncurry

@[to_additive]
protected theorem Multipliable.tprod_comm' {f : Œ≤ ‚Üí Œ≥ ‚Üí Œ±} (h : Multipliable (Function.uncurry f))
    (h‚ÇÅ : ‚àÄ b, Multipliable (f b)) (h‚ÇÇ : ‚àÄ c, Multipliable fun b ‚Ü¶ f b c) :
    ‚àè' (c) (b), f b c = ‚àè' (b) (c), f b c := by
  rw [‚Üê h.tprod_prod_uncurry h‚ÇÅ, ‚Üê h.prod_symm.tprod_prod_uncurry h‚ÇÇ,
    ‚Üê (Equiv.prodComm Œ≥ Œ≤).tprod_eq (uncurry f)]
  rfl

@[deprecated (since := "2025-04-12")] alias tsum_comm':= Summable.tsum_comm'
@[to_additive existing, deprecated (since := "2025-04-12")] alias
  tprod_comm' := Multipliable.tprod_comm'

end T3Space

end ContinuousMul

section CompleteSpace

variable [CommGroup Œ±] [UniformSpace Œ±] [IsUniformGroup Œ±]

@[to_additive]
theorem HasProd.of_sigma {Œ≥ : Œ≤ ‚Üí Type*} {f : (Œ£ b : Œ≤, Œ≥ b) ‚Üí Œ±} {g : Œ≤ ‚Üí Œ±} {a : Œ±}
    (hf : ‚àÄ b, HasProd (fun c ‚Ü¶ f ‚ü®b, c‚ü©) (g b)) (hg : HasProd g a)
    (h : CauchySeq (fun (s : Finset (Œ£ b : Œ≤, Œ≥ b)) ‚Ü¶ ‚àè i ‚àà s, f i)) :
    HasProd f a := by
  classical
  apply le_nhds_of_cauchy_adhp h
  simp only [‚Üê mapClusterPt_def, mapClusterPt_iff_frequently, frequently_atTop, ge_iff_le,
    le_eq_subset]
  intro u hu s
  rcases mem_nhds_iff.1 hu with ‚ü®v, vu, v_open, hv‚ü©
  obtain ‚ü®t0, st0, ht0‚ü© : ‚àÉ t0, ‚àè i ‚àà t0, g i ‚àà v ‚àß s.image Sigma.fst ‚äÜ t0 := by
    have A : ‚àÄ·∂† t0 in (atTop : Filter (Finset Œ≤)), ‚àè i ‚àà t0, g i ‚àà v := hg (v_open.mem_nhds hv)
    exact (A.and (Ici_mem_atTop _)).exists
  have L : Tendsto (fun t : Finset (Œ£ b, Œ≥ b) ‚Ü¶ ‚àè p ‚àà t with p.1 ‚àà t0, f p) atTop
      (ùìù <| ‚àè b ‚àà t0, g b) := by
    simp only [‚Üê sigma_preimage_mk, prod_sigma]
    refine tendsto_finset_prod _ fun b _ ‚Ü¶ ?_
    change
      Tendsto (fun t ‚Ü¶ (fun t ‚Ü¶ ‚àè s ‚àà t, f ‚ü®b, s‚ü©) (preimage t (Sigma.mk b) _)) atTop (ùìù (g b))
    exact (hf b).comp (tendsto_finset_preimage_atTop_atTop (sigma_mk_injective))
  have : ‚àÉ t, ‚àè p ‚àà t with p.1 ‚àà t0, f p ‚àà v ‚àß s ‚äÜ t :=
    ((Tendsto.eventually_mem L (v_open.mem_nhds st0)).and (Ici_mem_atTop _)).exists
  obtain ‚ü®t, tv, st‚ü© := this
  refine ‚ü®{p ‚àà t | p.1 ‚àà t0}, fun x hx ‚Ü¶ ?_, vu tv‚ü©
  simpa only [mem_filter, st hx, true_and] using ht0 (mem_image_of_mem Sigma.fst hx)

variable [CompleteSpace Œ±]

@[to_additive]
theorem Multipliable.sigma_factor {Œ≥ : Œ≤ ‚Üí Type*} {f : (Œ£ b : Œ≤, Œ≥ b) ‚Üí Œ±}
    (ha : Multipliable f) (b : Œ≤) :
    Multipliable fun c ‚Ü¶ f ‚ü®b, c‚ü© :=
  ha.comp_injective sigma_mk_injective

@[to_additive]
theorem Multipliable.sigma {Œ≥ : Œ≤ ‚Üí Type*} {f : (Œ£ b : Œ≤, Œ≥ b) ‚Üí Œ±} (ha : Multipliable f) :
    Multipliable fun b ‚Ü¶ ‚àè' c, f ‚ü®b, c‚ü© :=
  ha.sigma' fun b ‚Ü¶ ha.sigma_factor b

@[to_additive Summable.prod_factor]
theorem Multipliable.prod_factor {f : Œ≤ √ó Œ≥ ‚Üí Œ±} (h : Multipliable f) (b : Œ≤) :
    Multipliable fun c ‚Ü¶ f (b, c) :=
  h.comp_injective fun _ _ h ‚Ü¶ (Prod.ext_iff.1 h).2

@[to_additive Summable.prod]
lemma Multipliable.prod {f : Œ≤ √ó Œ≥ ‚Üí Œ±} (h : Multipliable f) :
    Multipliable fun b ‚Ü¶ ‚àè' c, f (b, c) :=
  ((Equiv.sigmaEquivProd Œ≤ Œ≥).multipliable_iff.mpr h).sigma

@[to_additive]
lemma HasProd.tprod_fiberwise [T2Space Œ±] {f : Œ≤ ‚Üí Œ±} {a : Œ±} (hf : HasProd f a) (g : Œ≤ ‚Üí Œ≥) :
    HasProd (fun c : Œ≥ ‚Ü¶ ‚àè' b : g ‚Åª¬π' {c}, f b) a :=
  (((Equiv.sigmaFiberEquiv g).hasProd_iff).mpr hf).sigma <|
    fun _ ‚Ü¶ ((hf.multipliable.subtype _).hasProd_iff).mpr rfl

section CompleteT0Space

variable [T0Space Œ±]

@[to_additive]
protected theorem Multipliable.tprod_sigma {Œ≥ : Œ≤ ‚Üí Type*} {f : (Œ£ b : Œ≤, Œ≥ b) ‚Üí Œ±}
    (ha : Multipliable f) : ‚àè' p, f p = ‚àè' (b) (c), f ‚ü®b, c‚ü© :=
  Multipliable.tprod_sigma' (fun b ‚Ü¶ ha.sigma_factor b) ha

@[deprecated (since := "2025-04-12")] alias tsum_sigma := Summable.tsum_sigma
@[to_additive existing, deprecated (since := "2025-04-12")] alias
  tprod_sigma := Multipliable.tprod_sigma

@[to_additive Summable.tsum_prod]
protected theorem Multipliable.tprod_prod {f : Œ≤ √ó Œ≥ ‚Üí Œ±} (h : Multipliable f) :
    ‚àè' p, f p = ‚àè' (b) (c), f ‚ü®b, c‚ü© :=
  h.tprod_prod' h.prod_factor

@[deprecated (since := "2025-04-12")] alias tsum_prod := Summable.tsum_prod
@[to_additive existing tsum_prod, deprecated (since := "2025-04-12")]
  alias tprod_prod := Multipliable.tprod_prod

@[to_additive]
protected theorem Multipliable.tprod_comm {f : Œ≤ ‚Üí Œ≥ ‚Üí Œ±} (h : Multipliable (Function.uncurry f)) :
    ‚àè' (c) (b), f b c = ‚àè' (b) (c), f b c :=
  h.tprod_comm' h.prod_factor h.prod_symm.prod_factor

@[deprecated (since := "2025-04-12")] alias tsum_comm := Summable.tsum_comm
@[to_additive existing, deprecated (since := "2025-04-12")] alias tprod_comm :=
  Multipliable.tprod_comm

end CompleteT0Space

end CompleteSpace

section Pi

variable {Œπ : Type*} {X : Œ± ‚Üí Type*} [‚àÄ x, CommMonoid (X x)] [‚àÄ x, TopologicalSpace (X x)]

@[to_additive]
theorem Pi.hasProd {f : Œπ ‚Üí ‚àÄ x, X x} {g : ‚àÄ x, X x} :
    HasProd f g ‚Üî ‚àÄ x, HasProd (fun i ‚Ü¶ f i x) (g x) := by
  simp only [HasProd, tendsto_pi_nhds, prod_apply]

@[to_additive]
theorem Pi.multipliable {f : Œπ ‚Üí ‚àÄ x, X x} : Multipliable f ‚Üî ‚àÄ x, Multipliable fun i ‚Ü¶ f i x := by
  simp only [Multipliable, Pi.hasProd, Classical.skolem]

@[to_additive]
theorem tprod_apply [‚àÄ x, T2Space (X x)] {f : Œπ ‚Üí ‚àÄ x, X x} {x : Œ±} (hf : Multipliable f) :
    (‚àè' i, f i) x = ‚àè' i, f i x :=
  (Pi.hasProd.mp hf.hasProd x).tprod_eq.symm

end Pi


/-! ## Multiplicative opposite -/

section MulOpposite

open MulOpposite

variable [AddCommMonoid Œ±] [TopologicalSpace Œ±] {f : Œ≤ ‚Üí Œ±} {a : Œ±}

theorem HasSum.op (hf : HasSum f a) : HasSum (fun a ‚Ü¶ op (f a)) (op a) :=
  (hf.map (@opAddEquiv Œ± _) continuous_op :)

theorem Summable.op (hf : Summable f) : Summable (op ‚àò f) :=
  hf.hasSum.op.summable

theorem HasSum.unop {f : Œ≤ ‚Üí Œ±·µê·µí·µñ} {a : Œ±·µê·µí·µñ} (hf : HasSum f a) :
    HasSum (fun a ‚Ü¶ unop (f a)) (unop a) :=
  (hf.map (@opAddEquiv Œ± _).symm continuous_unop :)

theorem Summable.unop {f : Œ≤ ‚Üí Œ±·µê·µí·µñ} (hf : Summable f) : Summable (unop ‚àò f) :=
  hf.hasSum.unop.summable

@[simp]
theorem hasSum_op : HasSum (fun a ‚Ü¶ op (f a)) (op a) ‚Üî HasSum f a :=
  ‚ü®HasSum.unop, HasSum.op‚ü©

@[simp]
theorem hasSum_unop {f : Œ≤ ‚Üí Œ±·µê·µí·µñ} {a : Œ±·µê·µí·µñ} :
    HasSum (fun a ‚Ü¶ unop (f a)) (unop a) ‚Üî HasSum f a :=
  ‚ü®HasSum.op, HasSum.unop‚ü©

@[simp]
theorem summable_op : (Summable fun a ‚Ü¶ op (f a)) ‚Üî Summable f :=
  ‚ü®Summable.unop, Summable.op‚ü©

theorem summable_unop {f : Œ≤ ‚Üí Œ±·µê·µí·µñ} : (Summable fun a ‚Ü¶ unop (f a)) ‚Üî Summable f :=
  ‚ü®Summable.op, Summable.unop‚ü©

theorem tsum_op [T2Space Œ±] :
    ‚àë' x, op (f x) = op (‚àë' x, f x) := by
  by_cases h : Summable f
  ¬∑ exact h.hasSum.op.tsum_eq
  ¬∑ have ho := summable_op.not.mpr h
    rw [tsum_eq_zero_of_not_summable h, tsum_eq_zero_of_not_summable ho, op_zero]

theorem tsum_unop [T2Space Œ±] {f : Œ≤ ‚Üí Œ±·µê·µí·µñ} :
    ‚àë' x, unop (f x) = unop (‚àë' x, f x) :=
  op_injective tsum_op.symm

end MulOpposite

/-! ## Interaction with the star -/

section ContinuousStar

variable [AddCommMonoid Œ±] [TopologicalSpace Œ±] [StarAddMonoid Œ±] [ContinuousStar Œ±] {f : Œ≤ ‚Üí Œ±}
  {a : Œ±}

theorem HasSum.star (h : HasSum f a) : HasSum (fun b ‚Ü¶ star (f b)) (star a) := by
  simpa only using h.map (starAddEquiv : Œ± ‚âÉ+ Œ±) continuous_star

theorem Summable.star (hf : Summable f) : Summable fun b ‚Ü¶ star (f b) :=
  hf.hasSum.star.summable

theorem Summable.ofStar (hf : Summable fun b ‚Ü¶ Star.star (f b)) : Summable f := by
  simpa only [star_star] using hf.star

@[simp]
theorem summable_star_iff : (Summable fun b ‚Ü¶ star (f b)) ‚Üî Summable f :=
  ‚ü®Summable.ofStar, Summable.star‚ü©

@[simp]
theorem summable_star_iff' : Summable (star f) ‚Üî Summable f :=
  summable_star_iff

theorem tsum_star [T2Space Œ±] : star (‚àë' b, f b) = ‚àë' b, star (f b) := by
  by_cases hf : Summable f
  ¬∑ exact hf.hasSum.star.tsum_eq.symm
  ¬∑ rw [tsum_eq_zero_of_not_summable hf, tsum_eq_zero_of_not_summable (mt Summable.ofStar hf),
      star_zero]

end ContinuousStar
