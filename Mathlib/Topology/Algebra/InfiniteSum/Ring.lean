/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl
-/
import Mathlib.Algebra.BigOperators.NatAntidiagonal
import Mathlib.Topology.Algebra.InfiniteSum.Basic
import Mathlib.Topology.Algebra.Ring.Basic

#align_import topology.algebra.infinite_sum.ring from "leanprover-community/mathlib"@"9a59dcb7a2d06bf55da57b9030169219980660cd"

/-!
# Infinite sum in a ring

This file provides lemmas about the interaction between infinite sums and multiplication.

## Main results

* `tsum_mul_tsum_eq_tsum_sum_antidiagonal`: Cauchy product formula
-/


open Filter Finset Function

open BigOperators Classical

variable {Î¹ Îº R Î± : Type*}

section NonUnitalNonAssocSemiring

variable [NonUnitalNonAssocSemiring Î±] [TopologicalSpace Î±] [TopologicalSemiring Î±] {f g : Î¹ â†’ Î±}
  {a aâ‚ aâ‚‚ : Î±}

theorem HasSum.mul_left (aâ‚‚) (h : HasSum f aâ‚) : HasSum (fun i => aâ‚‚ * f i) (aâ‚‚ * aâ‚) := by
  simpa only using h.map (AddMonoidHom.mulLeft aâ‚‚) (continuous_const.mul continuous_id)
  -- ðŸŽ‰ no goals
#align has_sum.mul_left HasSum.mul_left

theorem HasSum.mul_right (aâ‚‚) (hf : HasSum f aâ‚) : HasSum (fun i => f i * aâ‚‚) (aâ‚ * aâ‚‚) := by
  simpa only using hf.map (AddMonoidHom.mulRight aâ‚‚) (continuous_id.mul continuous_const)
  -- ðŸŽ‰ no goals
#align has_sum.mul_right HasSum.mul_right

theorem Summable.mul_left (a) (hf : Summable f) : Summable fun i => a * f i :=
  (hf.hasSum.mul_left _).summable
#align summable.mul_left Summable.mul_left

theorem Summable.mul_right (a) (hf : Summable f) : Summable fun i => f i * a :=
  (hf.hasSum.mul_right _).summable
#align summable.mul_right Summable.mul_right

section tsum

variable [T2Space Î±]

theorem Summable.tsum_mul_left (a) (hf : Summable f) : âˆ‘' i, a * f i = a * âˆ‘' i, f i :=
  (hf.hasSum.mul_left _).tsum_eq
#align summable.tsum_mul_left Summable.tsum_mul_left

theorem Summable.tsum_mul_right (a) (hf : Summable f) : âˆ‘' i, f i * a = (âˆ‘' i, f i) * a :=
  (hf.hasSum.mul_right _).tsum_eq
#align summable.tsum_mul_right Summable.tsum_mul_right

theorem Commute.tsum_right (a) (h : âˆ€ i, Commute a (f i)) : Commute a (âˆ‘' i, f i) :=
  if hf : Summable f then
    (hf.tsum_mul_left a).symm.trans ((congr_arg _ <| funext h).trans (hf.tsum_mul_right a))
  else (tsum_eq_zero_of_not_summable hf).symm â–¸ Commute.zero_right _
#align commute.tsum_right Commute.tsum_right

theorem Commute.tsum_left (a) (h : âˆ€ i, Commute (f i) a) : Commute (âˆ‘' i, f i) a :=
  (Commute.tsum_right _ fun i => (h i).symm).symm
#align commute.tsum_left Commute.tsum_left

end tsum

end NonUnitalNonAssocSemiring

section DivisionSemiring

variable [DivisionSemiring Î±] [TopologicalSpace Î±] [TopologicalSemiring Î±] {f g : Î¹ â†’ Î±}
  {a aâ‚ aâ‚‚ : Î±}

theorem HasSum.div_const (h : HasSum f a) (b : Î±) : HasSum (fun i => f i / b) (a / b) := by
  simp only [div_eq_mul_inv, h.mul_right bâ»Â¹]
  -- ðŸŽ‰ no goals
#align has_sum.div_const HasSum.div_const

theorem Summable.div_const (h : Summable f) (b : Î±) : Summable fun i => f i / b :=
  (h.hasSum.div_const _).summable
#align summable.div_const Summable.div_const

theorem hasSum_mul_left_iff (h : aâ‚‚ â‰  0) : HasSum (fun i => aâ‚‚ * f i) (aâ‚‚ * aâ‚) â†” HasSum f aâ‚ :=
  âŸ¨fun H => by simpa only [inv_mul_cancel_leftâ‚€ h] using H.mul_left aâ‚‚â»Â¹, HasSum.mul_left _âŸ©
               -- ðŸŽ‰ no goals
#align has_sum_mul_left_iff hasSum_mul_left_iff

theorem hasSum_mul_right_iff (h : aâ‚‚ â‰  0) : HasSum (fun i => f i * aâ‚‚) (aâ‚ * aâ‚‚) â†” HasSum f aâ‚ :=
  âŸ¨fun H => by simpa only [mul_inv_cancel_rightâ‚€ h] using H.mul_right aâ‚‚â»Â¹, HasSum.mul_right _âŸ©
               -- ðŸŽ‰ no goals
#align has_sum_mul_right_iff hasSum_mul_right_iff

theorem hasSum_div_const_iff (h : aâ‚‚ â‰  0) : HasSum (fun i => f i / aâ‚‚) (aâ‚ / aâ‚‚) â†” HasSum f aâ‚ := by
  simpa only [div_eq_mul_inv] using hasSum_mul_right_iff (inv_ne_zero h)
  -- ðŸŽ‰ no goals
#align has_sum_div_const_iff hasSum_div_const_iff

theorem summable_mul_left_iff (h : a â‰  0) : (Summable fun i => a * f i) â†” Summable f :=
  âŸ¨fun H => by simpa only [inv_mul_cancel_leftâ‚€ h] using H.mul_left aâ»Â¹, fun H => H.mul_left _âŸ©
               -- ðŸŽ‰ no goals
#align summable_mul_left_iff summable_mul_left_iff

theorem summable_mul_right_iff (h : a â‰  0) : (Summable fun i => f i * a) â†” Summable f :=
  âŸ¨fun H => by simpa only [mul_inv_cancel_rightâ‚€ h] using H.mul_right aâ»Â¹, fun H => H.mul_right _âŸ©
               -- ðŸŽ‰ no goals
#align summable_mul_right_iff summable_mul_right_iff

theorem summable_div_const_iff (h : a â‰  0) : (Summable fun i => f i / a) â†” Summable f := by
  simpa only [div_eq_mul_inv] using summable_mul_right_iff (inv_ne_zero h)
  -- ðŸŽ‰ no goals
#align summable_div_const_iff summable_div_const_iff

theorem tsum_mul_left [T2Space Î±] : âˆ‘' x, a * f x = a * âˆ‘' x, f x :=
  if hf : Summable f then hf.tsum_mul_left a
  else if ha : a = 0 then by simp [ha]
                             -- ðŸŽ‰ no goals
  else by rw [tsum_eq_zero_of_not_summable hf,
              tsum_eq_zero_of_not_summable (mt (summable_mul_left_iff ha).mp hf), mul_zero]
#align tsum_mul_left tsum_mul_left

theorem tsum_mul_right [T2Space Î±] : âˆ‘' x, f x * a = (âˆ‘' x, f x) * a :=
  if hf : Summable f then hf.tsum_mul_right a
  else if ha : a = 0 then by simp [ha]
                             -- ðŸŽ‰ no goals
  else by rw [tsum_eq_zero_of_not_summable hf,
              tsum_eq_zero_of_not_summable (mt (summable_mul_right_iff ha).mp hf), zero_mul]
#align tsum_mul_right tsum_mul_right

theorem tsum_div_const [T2Space Î±] : âˆ‘' x, f x / a = (âˆ‘' x, f x) / a := by
  simpa only [div_eq_mul_inv] using tsum_mul_right
  -- ðŸŽ‰ no goals
#align tsum_div_const tsum_div_const

end DivisionSemiring

/-!
### Multipliying two infinite sums

In this section, we prove various results about `(âˆ‘' x : Î¹, f x) * (âˆ‘' y : Îº, g y)`. Note that we
always assume that the family `Î» x : Î¹ Ã— Îº, f x.1 * g x.2` is summable, since there is no way to
deduce this from the summabilities of `f` and `g` in general, but if you are working in a normed
space, you may want to use the analogous lemmas in `Analysis/NormedSpace/Basic`
(e.g `tsum_mul_tsum_of_summable_norm`).

We first establish results about arbitrary index types, `Î¹` and `Îº`, and then we specialize to
`Î¹ = Îº = â„•` to prove the Cauchy product formula (see `tsum_mul_tsum_eq_tsum_sum_antidiagonal`).

#### Arbitrary index types
-/


section tsum_mul_tsum

variable [TopologicalSpace Î±] [T3Space Î±] [NonUnitalNonAssocSemiring Î±] [TopologicalSemiring Î±]
  {f : Î¹ â†’ Î±} {g : Îº â†’ Î±} {s t u : Î±}

theorem HasSum.mul_eq (hf : HasSum f s) (hg : HasSum g t)
    (hfg : HasSum (fun x : Î¹ Ã— Îº => f x.1 * g x.2) u) : s * t = u :=
  have keyâ‚ : HasSum (fun i => f i * t) (s * t) := hf.mul_right t
  have this : âˆ€ i : Î¹, HasSum (fun c : Îº => f i * g c) (f i * t) := fun i => hg.mul_left (f i)
  have keyâ‚‚ : HasSum (fun i => f i * t) u := HasSum.prod_fiberwise hfg this
  keyâ‚.unique keyâ‚‚
#align has_sum.mul_eq HasSum.mul_eq

theorem HasSum.mul (hf : HasSum f s) (hg : HasSum g t)
    (hfg : Summable fun x : Î¹ Ã— Îº => f x.1 * g x.2) :
    HasSum (fun x : Î¹ Ã— Îº => f x.1 * g x.2) (s * t) :=
  let âŸ¨_u, huâŸ© := hfg
  (hf.mul_eq hg hu).symm â–¸ hu
#align has_sum.mul HasSum.mul

/-- Product of two infinites sums indexed by arbitrary types.
    See also `tsum_mul_tsum_of_summable_norm` if `f` and `g` are absolutely summable. -/
theorem tsum_mul_tsum (hf : Summable f) (hg : Summable g)
    (hfg : Summable fun x : Î¹ Ã— Îº => f x.1 * g x.2) :
    ((âˆ‘' x, f x) * âˆ‘' y, g y) = âˆ‘' z : Î¹ Ã— Îº, f z.1 * g z.2 :=
  hf.hasSum.mul_eq hg.hasSum hfg.hasSum
#align tsum_mul_tsum tsum_mul_tsum

end tsum_mul_tsum

/-!
#### `â„•`-indexed families (Cauchy product)

We prove two versions of the Cauchy product formula. The first one is
`tsum_mul_tsum_eq_tsum_sum_range`, where the `n`-th term is a sum over `Finset.range (n+1)`
involving `Nat` subtraction.
In order to avoid `Nat` subtraction, we also provide `tsum_mul_tsum_eq_tsum_sum_antidiagonal`,
where the `n`-th term is a sum over all pairs `(k, l)` such that `k+l=n`, which corresponds to the
`Finset` `Finset.Nat.antidiagonal n`
-/


section CauchyProduct

variable [TopologicalSpace Î±] [NonUnitalNonAssocSemiring Î±] {f g : â„• â†’ Î±}

/- The family `(k, l) : â„• Ã— â„• â†¦ f k * g l` is summable if and only if the family
`(n, k, l) : Î£ (n : â„•), Nat.antidiagonal n â†¦ f k * g l` is summable. -/
theorem summable_mul_prod_iff_summable_mul_sigma_antidiagonal :
    (Summable fun x : â„• Ã— â„• => f x.1 * g x.2) â†”
      Summable fun x : Î£n : â„•, Nat.antidiagonal n => f (x.2 : â„• Ã— â„•).1 * g (x.2 : â„• Ã— â„•).2 :=
  Nat.sigmaAntidiagonalEquivProd.summable_iff.symm
#align summable_mul_prod_iff_summable_mul_sigma_antidiagonal summable_mul_prod_iff_summable_mul_sigma_antidiagonal

variable [T3Space Î±] [TopologicalSemiring Î±]

theorem summable_sum_mul_antidiagonal_of_summable_mul
    (h : Summable fun x : â„• Ã— â„• => f x.1 * g x.2) :
    Summable fun n => âˆ‘ kl in Nat.antidiagonal n, f kl.1 * g kl.2 := by
  rw [summable_mul_prod_iff_summable_mul_sigma_antidiagonal] at h
  -- âŠ¢ Summable fun n => âˆ‘ kl in Nat.antidiagonal n, f kl.fst * g kl.snd
  conv => congr; ext; rw [â† Finset.sum_finset_coe, â† tsum_fintype]
  -- âŠ¢ Summable fun x => âˆ‘' (b : â†‘â†‘(Nat.antidiagonal x)), f (â†‘b).fst * g (â†‘b).snd
  exact h.sigma' fun n => (hasSum_fintype _).summable
  -- ðŸŽ‰ no goals
#align summable_sum_mul_antidiagonal_of_summable_mul summable_sum_mul_antidiagonal_of_summable_mul

/-- The **Cauchy product formula** for the product of two infinites sums indexed by `â„•`, expressed
by summing on `Finset.Nat.antidiagonal`.

See also `tsum_mul_tsum_eq_tsum_sum_antidiagonal_of_summable_norm` if `f` and `g` are absolutely
summable. -/
theorem tsum_mul_tsum_eq_tsum_sum_antidiagonal (hf : Summable f) (hg : Summable g)
    (hfg : Summable fun x : â„• Ã— â„• => f x.1 * g x.2) :
    ((âˆ‘' n, f n) * âˆ‘' n, g n) = âˆ‘' n, âˆ‘ kl in Nat.antidiagonal n, f kl.1 * g kl.2 := by
  conv_rhs => congr; ext; rw [â† Finset.sum_finset_coe, â† tsum_fintype]
  -- âŠ¢ (âˆ‘' (n : â„•), f n) * âˆ‘' (n : â„•), g n = âˆ‘' (x : â„•) (b : â†‘â†‘(Nat.antidiagonal x) â€¦
  rw [tsum_mul_tsum hf hg hfg, â† Nat.sigmaAntidiagonalEquivProd.tsum_eq (_ : â„• Ã— â„• â†’ Î±)]
  -- âŠ¢ âˆ‘' (c : (n : â„•) Ã— { x // x âˆˆ Nat.antidiagonal n }), f (â†‘Nat.sigmaAntidiagona â€¦
  exact
    tsum_sigma' (fun n => (hasSum_fintype _).summable)
      (summable_mul_prod_iff_summable_mul_sigma_antidiagonal.mp hfg)
#align tsum_mul_tsum_eq_tsum_sum_antidiagonal tsum_mul_tsum_eq_tsum_sum_antidiagonal

theorem summable_sum_mul_range_of_summable_mul (h : Summable fun x : â„• Ã— â„• => f x.1 * g x.2) :
    Summable fun n => âˆ‘ k in range (n + 1), f k * g (n - k) := by
  simp_rw [â† Nat.sum_antidiagonal_eq_sum_range_succ fun k l => f k * g l]
  -- âŠ¢ Summable fun n => âˆ‘ ij in Nat.antidiagonal n, f ij.fst * g ij.snd
  exact summable_sum_mul_antidiagonal_of_summable_mul h
  -- ðŸŽ‰ no goals
#align summable_sum_mul_range_of_summable_mul summable_sum_mul_range_of_summable_mul

/-- The **Cauchy product formula** for the product of two infinites sums indexed by `â„•`, expressed
by summing on `Finset.range`.

See also `tsum_mul_tsum_eq_tsum_sum_range_of_summable_norm` if `f` and `g` are absolutely summable.
-/
theorem tsum_mul_tsum_eq_tsum_sum_range (hf : Summable f) (hg : Summable g)
    (hfg : Summable fun x : â„• Ã— â„• => f x.1 * g x.2) :
    ((âˆ‘' n, f n) * âˆ‘' n, g n) = âˆ‘' n, âˆ‘ k in range (n + 1), f k * g (n - k) := by
  simp_rw [â† Nat.sum_antidiagonal_eq_sum_range_succ fun k l => f k * g l]
  -- âŠ¢ (âˆ‘' (n : â„•), f n) * âˆ‘' (n : â„•), g n = âˆ‘' (n : â„•), âˆ‘ ij in Nat.antidiagonal n â€¦
  exact tsum_mul_tsum_eq_tsum_sum_antidiagonal hf hg hfg
  -- ðŸŽ‰ no goals
#align tsum_mul_tsum_eq_tsum_sum_range tsum_mul_tsum_eq_tsum_sum_range

end CauchyProduct
