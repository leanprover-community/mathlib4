/-
Copyright (c) 2023 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Topology.MetricSpace.Baire

/-! # Open mapping theorem for morphisms of topological groups

We prove that a continuous surjective group morphism from a sigma-compact group to a locally compact
group is automatically open, in `MonoidHom.isOpenMap_of_sigmaCompact`.

We deduce this from a similar statement for the orbits of continuous actions of sigma-compact groups
on Baire spaces, given in `isOpenMap_smul_of_sigmaCompact`.
-/

open scoped Topology Pointwise

open MulAction Set Function

variable {G X : Type*} [TopologicalSpace G] [TopologicalSpace X]
  [Group G] [TopologicalGroup G] [MulAction G X]
  [SigmaCompactSpace G] [BaireSpace X] [T2Space X]
  [ContinuousSMul G X] [IsPretransitive G X]

/-- Consider a sigma-compact group acting continuously and transitively on a Baire space. Then
the orbit map is open around the identity. It follows in `isOpenMap_smul_of_sigmaCompact` that it
is open around any point. -/
@[to_additive "Consider a sigma-compact additive group acting continuously and transitively on a
Baire space. Then the orbit map is open around zero. It follows in
`isOpenMap_vadd_of_sigmaCompact` that it is open around any point."]
theorem smul_singleton_mem_nhds_of_sigmaCompact
    {U : Set G} (hU : U ‚àà ùìù 1) (x : X) : U ‚Ä¢ {x} ‚àà ùìù x := by
  /- Consider a small closed neighborhood `V` of the identity. Then the group is covered by
  countably many translates of `V`, say `g·µ¢ V`. Let also `K‚Çô` be a sequence of compact sets covering
  the space. Then the image of `K‚Çô ‚à© g·µ¢ V` in the orbit is compact, and their unions covers the
  space. By Baire, one of them has nonempty interior. Then `g·µ¢ V ‚Ä¢ x` has nonempty interior, and
  so does `V ‚Ä¢ x`. Its interior contains a point `g' x` with `g' ‚àà V`. Then `g'‚Åª¬π ‚Ä¢ V ‚Ä¢ x` contains
  a neighborhood of `x`, and it is included in `V‚Åª¬π ‚Ä¢ V ‚Ä¢ x`, which is itself contained in `U ‚Ä¢ x`
  if `V` is small enough. -/
  obtain ‚ü®V, V_mem, V_closed, V_symm, VU‚ü© : ‚àÉ V ‚àà ùìù (1 : G), IsClosed V ‚àß V‚Åª¬π = V ‚àß V * V ‚äÜ U :=
    exists_nhds_one_isClosed_inv_eq_mul_subset hU
  obtain ‚ü®s, s_count, hs‚ü© : ‚àÉ (s : Set G), s.Countable ‚àß ‚ãÉ g ‚àà s, g ‚Ä¢ V = univ := by
    apply countable_cover_nhds_of_sigma_compact (fun g ‚Ü¶ ?_)
    convert smul_mem_nhds g V_mem
    simp only [smul_eq_mul, mul_one]
  let K : ‚Ñï ‚Üí Set G := compactCovering G
  let F : ‚Ñï √ó s ‚Üí Set X := fun p ‚Ü¶ (K p.1 ‚à© (p.2 : G) ‚Ä¢ V) ‚Ä¢ ({x} : Set X)
  obtain ‚ü®‚ü®n, ‚ü®g, hg‚ü©‚ü©, hi‚ü© : ‚àÉ i, (interior (F i)).Nonempty := by
    have : Nonempty X := ‚ü®x‚ü©
    have : Encodable s := Countable.toEncodable s_count
    apply nonempty_interior_of_iUnion_of_closed
    ¬∑ rintro ‚ü®n, ‚ü®g, hg‚ü©‚ü©
      apply IsCompact.isClosed
      suffices H : IsCompact ((fun (g : G) ‚Ü¶ g ‚Ä¢ x) '' (K n ‚à© g ‚Ä¢ V)) by
        simpa only [smul_singleton] using H
      apply IsCompact.image
      ¬∑ exact (isCompact_compactCovering G n).inter_right (V_closed.smul g)
      ¬∑ exact continuous_id.smul continuous_const
    ¬∑ apply eq_univ_iff_forall.2 (fun y ‚Ü¶ ?_)
      obtain ‚ü®h, rfl‚ü© : ‚àÉ h, h ‚Ä¢ x = y := exists_smul_eq G x y
      obtain ‚ü®n, hn‚ü© : ‚àÉ n, h ‚àà K n := exists_mem_compactCovering h
      obtain ‚ü®g, gs, hg‚ü© : ‚àÉ g ‚àà s, h ‚àà g ‚Ä¢ V := exists_set_mem_of_union_eq_top s _ hs _
      simp only [smul_singleton, mem_iUnion, mem_image, mem_inter_iff, Prod.exists, Subtype.exists,
        exists_prop]
      exact ‚ü®n, g, gs, h, ‚ü®hn, hg‚ü©, rfl‚ü©
  have I : (interior ((g ‚Ä¢ V) ‚Ä¢ {x})).Nonempty := by
    apply hi.mono
    apply interior_mono
    exact smul_subset_smul_right (inter_subset_right _ _)
  obtain ‚ü®y, hy‚ü© : (interior (V ‚Ä¢ ({x} : Set X))).Nonempty := by
    rw [smul_assoc, interior_smul] at I
    exact smul_set_nonempty.1 I
  obtain ‚ü®g', hg', rfl‚ü© : ‚àÉ g' ‚àà V, g' ‚Ä¢ x = y := by simpa using interior_subset hy
  have J : (g' ‚Åª¬π ‚Ä¢ V) ‚Ä¢ {x} ‚àà ùìù x := by
    apply mem_interior_iff_mem_nhds.1
    rwa [smul_assoc, interior_smul, mem_inv_smul_set_iff]
  have : (g'‚Åª¬π ‚Ä¢ V) ‚Ä¢ {x} ‚äÜ U ‚Ä¢ ({x} : Set X) := by
    apply smul_subset_smul_right
    apply Subset.trans (smul_set_subset_smul (inv_mem_inv.2 hg')) ?_
    rw [V_symm]
    exact VU
  exact Filter.mem_of_superset J this

/-- Consider a sigma-compact group acting continuously and transitively on a Baire space. Then
the orbit map is open. This is a version of the open mapping theorem, valid notably for the
action of a sigma-compact locally compact group on a locally compact space. -/
@[to_additive "Consider a sigma-compact additive group acting continuously and transitively on a
Baire space. Then the orbit map is open. This is a version of the open mapping theorem, valid
notably for the action of a sigma-compact locally compact group on a locally compact space."]
theorem isOpenMap_smul_of_sigmaCompact (x : X) : IsOpenMap (fun (g : G) ‚Ü¶ g ‚Ä¢ x) := by
  /- We have already proved the theorem around the basepoint of the orbit, in
  `smul_singleton_mem_nhds_of_sigmaCompact`. The general statement follows around an arbitrary
  point by changing basepoints. -/
  intro U hU
  apply isOpen_iff_forall_mem_open.2
  rintro - ‚ü®g, gU, rfl‚ü©
  let V := (fun x ‚Ü¶ x * g) ‚Åª¬π' U
  have A : V ‚àà ùìù 1 := by
    apply (continuous_mul_right g).continuousAt.preimage_mem_nhds
    apply hU.mem_nhds
    simpa using gU
  obtain ‚ü®t, ht, t_open, gt‚ü© : ‚àÉ t, t ‚äÜ V ‚Ä¢ {g ‚Ä¢ x} ‚àß IsOpen t ‚àß g ‚Ä¢ x ‚àà t := by
    have : V ‚Ä¢ {g ‚Ä¢ x} ‚àà ùìù (g ‚Ä¢ x) := smul_singleton_mem_nhds_of_sigmaCompact A _
    rw [mem_nhds_iff] at this
    exact this
  refine ‚ü®t, ht.trans ?_, t_open, gt‚ü©
  rintro - ‚ü®t, -, ht, rfl, rfl‚ü©
  exact ‚ü®t ‚Ä¢ g, by simpa using ht, by simp [‚Üê smul_assoc]‚ü©

/-- A surjective morphism of topological groups is open when the source group is sigma-compact and
the target group is a Baire space (for instance a locally compact group). -/
@[to_additive]
theorem MonoidHom.isOpenMap_of_sigmaCompact
    {H : Type*} [Group H] [TopologicalSpace H] [BaireSpace H] [T2Space H] [ContinuousMul H]
    (f : G ‚Üí* H) (hf : Function.Surjective f) (h'f : Continuous f) :
    IsOpenMap f := by
  let A : MulAction G H := MulAction.compHom _ f
  have : ContinuousSMul G H := continuousSMul_compHom h'f
  have : IsPretransitive G H := isPretransitive_compHom hf
  have : f = (fun (g : G) ‚Ü¶ g ‚Ä¢ (1 : H)) := by simp [MulAction.compHom_smul_def]
  rw [this]
  exact isOpenMap_smul_of_sigmaCompact _
