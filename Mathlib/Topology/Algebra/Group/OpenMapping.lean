/-
Copyright (c) 2023 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Topology.Baire.Lemmas
import Mathlib.Topology.Algebra.Group.Basic

/-! # Open mapping theorem for morphisms of topological groups

We prove that a continuous surjective group morphism from a sigma-compact group to a locally compact
group is automatically open, in `MonoidHom.isOpenMap_of_sigmaCompact`.

We deduce this from a similar statement for the orbits of continuous actions of sigma-compact groups
on Baire spaces, given in `isOpenMap_smul_of_sigmaCompact`.

Note that a sigma-compactness assumption is necessary. Indeed, let `G` be the real line with
the discrete topology, and `H` the real line with the usual topology. Both are locally compact
groups, and the identity from `G` to `H` is continuous but not open.
-/

open scoped Topology Pointwise

open MulAction Set Function

variable {G X : Type*} [TopologicalSpace G] [TopologicalSpace X]
  [Group G] [TopologicalGroup G] [MulAction G X]
  [SigmaCompactSpace G] [BaireSpace X] [T2Space X]
  [ContinuousSMul G X] [IsPretransitive G X]

/-- Consider a sigma-compact group acting continuously and transitively on a Baire space. Then
the orbit map is open around the identity. It follows in `isOpenMap_smul_of_sigmaCompact` that it
is open around any point. -/
@[to_additive "Consider a sigma-compact additive group acting continuously and transitively on a
Baire space. Then the orbit map is open around zero. It follows in
`isOpenMap_vadd_of_sigmaCompact` that it is open around any point."]
theorem smul_singleton_mem_nhds_of_sigmaCompact
    {U : Set G} (hU : U âˆˆ ð“ 1) (x : X) : U â€¢ {x} âˆˆ ð“ x := by
  /- Consider a small closed neighborhood `V` of the identity. Then the group is covered by
  countably many translates of `V`, say `gáµ¢ V`. Let also `Kâ‚™` be a sequence of compact sets covering
  the space. Then the image of `Kâ‚™ âˆ© gáµ¢ V` in the orbit is compact, and their unions covers the
  space. By Baire, one of them has nonempty interior. Then `gáµ¢ V â€¢ x` has nonempty interior, and
  so does `V â€¢ x`. Its interior contains a point `g' x` with `g' âˆˆ V`. Then `g'â»Â¹ â€¢ V â€¢ x` contains
  a neighborhood of `x`, and it is included in `Vâ»Â¹ â€¢ V â€¢ x`, which is itself contained in `U â€¢ x`
  if `V` is small enough. -/
  obtain âŸ¨V, V_mem, V_closed, V_symm, VUâŸ© : âˆƒ V âˆˆ ð“ (1 : G), IsClosed V âˆ§ Vâ»Â¹ = V âˆ§ V * V âŠ† U :=
    exists_closed_nhds_one_inv_eq_mul_subset hU
  obtain âŸ¨s, s_count, hsâŸ© : âˆƒ (s : Set G), s.Countable âˆ§ â‹ƒ g âˆˆ s, g â€¢ V = univ := by
    apply countable_cover_nhds_of_sigma_compact (fun g â†¦ ?_)
    convert smul_mem_nhds g V_mem
    simp only [smul_eq_mul, mul_one]
  let K : â„• â†’ Set G := compactCovering G
  let F : â„• Ã— s â†’ Set X := fun p â†¦ (K p.1 âˆ© (p.2 : G) â€¢ V) â€¢ ({x} : Set X)
  obtain âŸ¨âŸ¨n, âŸ¨g, hgâŸ©âŸ©, hiâŸ© : âˆƒ i, (interior (F i)).Nonempty := by
    have : Nonempty X := âŸ¨xâŸ©
    have : Encodable s := Countable.toEncodable s_count
    apply nonempty_interior_of_iUnion_of_closed
    Â· rintro âŸ¨n, âŸ¨g, hgâŸ©âŸ©
      apply IsCompact.isClosed
      suffices H : IsCompact ((fun (g : G) â†¦ g â€¢ x) '' (K n âˆ© g â€¢ V)) by
        simpa only [F, smul_singleton] using H
      apply IsCompact.image
      Â· exact (isCompact_compactCovering G n).inter_right (V_closed.smul g)
      Â· exact continuous_id.smul continuous_const
    Â· apply eq_univ_iff_forall.2 (fun y â†¦ ?_)
      obtain âŸ¨h, rflâŸ© : âˆƒ h, h â€¢ x = y := exists_smul_eq G x y
      obtain âŸ¨n, hnâŸ© : âˆƒ n, h âˆˆ K n := exists_mem_compactCovering h
      obtain âŸ¨g, gs, hgâŸ© : âˆƒ g âˆˆ s, h âˆˆ g â€¢ V := exists_set_mem_of_union_eq_top s _ hs _
      simp only [F, smul_singleton, mem_iUnion, mem_image, mem_inter_iff, Prod.exists,
        Subtype.exists, exists_prop]
      exact âŸ¨n, g, gs, h, âŸ¨hn, hgâŸ©, rflâŸ©
  have I : (interior ((g â€¢ V) â€¢ {x})).Nonempty := by
    apply hi.mono
    apply interior_mono
    exact smul_subset_smul_right (inter_subset_right _ _)
  obtain âŸ¨y, hyâŸ© : (interior (V â€¢ ({x} : Set X))).Nonempty := by
    rw [smul_assoc, interior_smul] at I
    exact smul_set_nonempty.1 I
  obtain âŸ¨g', hg', rflâŸ© : âˆƒ g' âˆˆ V, g' â€¢ x = y := by simpa using interior_subset hy
  have J : (g' â»Â¹ â€¢ V) â€¢ {x} âˆˆ ð“ x := by
    apply mem_interior_iff_mem_nhds.1
    rwa [smul_assoc, interior_smul, mem_inv_smul_set_iff]
  have : (g'â»Â¹ â€¢ V) â€¢ {x} âŠ† U â€¢ ({x} : Set X) := by
    apply smul_subset_smul_right
    apply Subset.trans (smul_set_subset_smul (inv_mem_inv.2 hg')) ?_
    rw [V_symm]
    exact VU
  exact Filter.mem_of_superset J this

/-- Consider a sigma-compact group acting continuously and transitively on a Baire space. Then
the orbit map is open. This is a version of the open mapping theorem, valid notably for the
action of a sigma-compact locally compact group on a locally compact space. -/
@[to_additive "Consider a sigma-compact additive group acting continuously and transitively on a
Baire space. Then the orbit map is open. This is a version of the open mapping theorem, valid
notably for the action of a sigma-compact locally compact group on a locally compact space."]
theorem isOpenMap_smul_of_sigmaCompact (x : X) : IsOpenMap (fun (g : G) â†¦ g â€¢ x) := by
  /- We have already proved the theorem around the basepoint of the orbit, in
  `smul_singleton_mem_nhds_of_sigmaCompact`. The general statement follows around an arbitrary
  point by changing basepoints. -/
  simp_rw [isOpenMap_iff_nhds_le, Filter.le_map_iff]
  intro g U hU
  have : (Â· â€¢ x) = (Â· â€¢ (g â€¢ x)) âˆ˜ (Â· * gâ»Â¹) := by
    ext g
    simp [smul_smul]
  rw [this, image_comp, â† smul_singleton]
  apply smul_singleton_mem_nhds_of_sigmaCompact
  simpa using isOpenMap_mul_right gâ»Â¹ |>.image_mem_nhds hU

/-- A surjective morphism of topological groups is open when the source group is sigma-compact and
the target group is a Baire space (for instance a locally compact group). -/
@[to_additive]
theorem MonoidHom.isOpenMap_of_sigmaCompact
    {H : Type*} [Group H] [TopologicalSpace H] [BaireSpace H] [T2Space H] [ContinuousMul H]
    (f : G â†’* H) (hf : Function.Surjective f) (h'f : Continuous f) :
    IsOpenMap f := by
  let A : MulAction G H := MulAction.compHom _ f
  have : ContinuousSMul G H := continuousSMul_compHom h'f
  have : IsPretransitive G H := isPretransitive_compHom hf
  have : f = (fun (g : G) â†¦ g â€¢ (1 : H)) := by simp [MulAction.compHom_smul_def]
  rw [this]
  exact isOpenMap_smul_of_sigmaCompact _
