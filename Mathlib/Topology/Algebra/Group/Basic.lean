/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Patrick Massot
-/
import Mathlib.GroupTheory.GroupAction.ConjAct
import Mathlib.GroupTheory.GroupAction.Quotient
import Mathlib.Topology.Algebra.Monoid
import Mathlib.Topology.Algebra.Constructions
import Mathlib.Algebra.Order.Archimedean.Basic
import Mathlib.GroupTheory.QuotientGroup.Basic

/-!
# Topological groups

This file defines the following typeclasses:

* `TopologicalGroup`, `TopologicalAddGroup`: multiplicative and additive topological groups,
  i.e., groups with continuous `(*)` and `(â»Â¹)` / `(+)` and `(-)`;

* `ContinuousSub G` means that `G` has a continuous subtraction operation.

There is an instance deducing `ContinuousSub` from `TopologicalGroup` but we use a separate
typeclass because, e.g., `â„•` and `â„â‰¥0` have continuous subtraction but are not additive groups.

We also define `Homeomorph` versions of several `Equiv`s: `Homeomorph.mulLeft`,
`Homeomorph.mulRight`, `Homeomorph.inv`, and prove a few facts about neighbourhood filters in
groups.

## Tags

topological space, group, topological group
-/

open Set Filter TopologicalSpace Function Topology Pointwise MulOpposite

universe u v w x

variable {G : Type w} {H : Type x} {Î± : Type u} {Î² : Type v}

section ContinuousMulGroup

/-!
### Groups with continuous multiplication

In this section we prove a few statements about groups with continuous `(*)`.
-/


variable [TopologicalSpace G] [Group G] [ContinuousMul G]

/-- Multiplication from the left in a topological group as a homeomorphism. -/
@[to_additive "Addition from the left in a topological additive group as a homeomorphism."]
protected def Homeomorph.mulLeft (a : G) : G â‰ƒâ‚œ G :=
  { Equiv.mulLeft a with
    continuous_toFun := continuous_const.mul continuous_id
    continuous_invFun := continuous_const.mul continuous_id }

@[to_additive (attr := simp)]
theorem Homeomorph.coe_mulLeft (a : G) : â‡‘(Homeomorph.mulLeft a) = (a * Â·) :=
  rfl

@[to_additive]
theorem Homeomorph.mulLeft_symm (a : G) : (Homeomorph.mulLeft a).symm = Homeomorph.mulLeft aâ»Â¹ := by
  ext
  rfl

@[to_additive]
lemma isOpenMap_mul_left (a : G) : IsOpenMap (a * Â·) := (Homeomorph.mulLeft a).isOpenMap

@[to_additive IsOpen.left_addCoset]
theorem IsOpen.leftCoset {U : Set G} (h : IsOpen U) (x : G) : IsOpen (x â€¢ U) :=
  isOpenMap_mul_left x _ h

@[to_additive]
lemma isClosedMap_mul_left (a : G) : IsClosedMap (a * Â·) := (Homeomorph.mulLeft a).isClosedMap

@[to_additive IsClosed.left_addCoset]
theorem IsClosed.leftCoset {U : Set G} (h : IsClosed U) (x : G) : IsClosed (x â€¢ U) :=
  isClosedMap_mul_left x _ h

/-- Multiplication from the right in a topological group as a homeomorphism. -/
@[to_additive "Addition from the right in a topological additive group as a homeomorphism."]
protected def Homeomorph.mulRight (a : G) : G â‰ƒâ‚œ G :=
  { Equiv.mulRight a with
    continuous_toFun := continuous_id.mul continuous_const
    continuous_invFun := continuous_id.mul continuous_const }

@[to_additive (attr := simp)]
lemma Homeomorph.coe_mulRight (a : G) : â‡‘(Homeomorph.mulRight a) = (Â· * a) := rfl

@[to_additive]
theorem Homeomorph.mulRight_symm (a : G) :
    (Homeomorph.mulRight a).symm = Homeomorph.mulRight aâ»Â¹ := by
  ext
  rfl

@[to_additive]
theorem isOpenMap_mul_right (a : G) : IsOpenMap (Â· * a) :=
  (Homeomorph.mulRight a).isOpenMap

@[to_additive IsOpen.right_addCoset]
theorem IsOpen.rightCoset {U : Set G} (h : IsOpen U) (x : G) : IsOpen (op x â€¢ U) :=
  isOpenMap_mul_right x _ h

@[to_additive]
theorem isClosedMap_mul_right (a : G) : IsClosedMap (Â· * a) :=
  (Homeomorph.mulRight a).isClosedMap

@[to_additive IsClosed.right_addCoset]
theorem IsClosed.rightCoset {U : Set G} (h : IsClosed U) (x : G) : IsClosed (op x â€¢ U) :=
  isClosedMap_mul_right x _ h

@[to_additive]
theorem discreteTopology_of_isOpen_singleton_one (h : IsOpen ({1} : Set G)) :
    DiscreteTopology G := by
  rw [â† singletons_open_iff_discrete]
  intro g
  suffices {g} = (gâ»Â¹ * Â·) â»Â¹' {1} by
    rw [this]
    exact (continuous_mul_left gâ»Â¹).isOpen_preimage _ h
  simp only [mul_one, Set.preimage_mul_left_singleton, eq_self_iff_true, inv_inv,
    Set.singleton_eq_singleton_iff]

@[to_additive]
theorem discreteTopology_iff_isOpen_singleton_one : DiscreteTopology G â†” IsOpen ({1} : Set G) :=
  âŸ¨fun h => forall_open_iff_discrete.mpr h {1}, discreteTopology_of_isOpen_singleton_oneâŸ©

end ContinuousMulGroup

/-!
### `ContinuousInv` and `ContinuousNeg`
-/


/-- Basic hypothesis to talk about a topological additive group. A topological additive group
over `M`, for example, is obtained by requiring the instances `AddGroup M` and
`ContinuousAdd M` and `ContinuousNeg M`. -/
class ContinuousNeg (G : Type u) [TopologicalSpace G] [Neg G] : Prop where
  continuous_neg : Continuous fun a : G => -a

attribute [continuity, fun_prop] ContinuousNeg.continuous_neg

/-- Basic hypothesis to talk about a topological group. A topological group over `M`, for example,
is obtained by requiring the instances `Group M` and `ContinuousMul M` and
`ContinuousInv M`. -/
@[to_additive (attr := continuity)]
class ContinuousInv (G : Type u) [TopologicalSpace G] [Inv G] : Prop where
  continuous_inv : Continuous fun a : G => aâ»Â¹

attribute [continuity, fun_prop] ContinuousInv.continuous_inv

export ContinuousInv (continuous_inv)

export ContinuousNeg (continuous_neg)

section ContinuousInv

variable [TopologicalSpace G] [Inv G] [ContinuousInv G]

@[to_additive]
theorem ContinuousInv.induced {Î± : Type*} {Î² : Type*} {F : Type*} [FunLike F Î± Î²] [Group Î±]
    [Group Î²] [MonoidHomClass F Î± Î²] [tÎ² : TopologicalSpace Î²] [ContinuousInv Î²] (f : F) :
    @ContinuousInv Î± (tÎ².induced f) _ := by
  let _tÎ± := tÎ².induced f
  refine âŸ¨continuous_induced_rng.2 ?_âŸ©
  simp only [Function.comp_def, map_inv]
  fun_prop

@[to_additive]
protected theorem Specializes.inv {x y : G} (h : x â¤³ y) : (xâ»Â¹) â¤³ (yâ»Â¹) :=
  h.map continuous_inv

@[to_additive]
protected theorem Inseparable.inv {x y : G} (h : Inseparable x y) : Inseparable (xâ»Â¹) (yâ»Â¹) :=
  h.map continuous_inv

@[to_additive]
protected theorem Specializes.zpow {G : Type*} [DivInvMonoid G] [TopologicalSpace G]
    [ContinuousMul G] [ContinuousInv G] {x y : G} (h : x â¤³ y) : âˆ€ m : â„¤, (x ^ m) â¤³ (y ^ m)
  | .ofNat n => by simpa using h.pow n
  | .negSucc n => by simpa using (h.pow (n + 1)).inv

@[to_additive]
protected theorem Inseparable.zpow {G : Type*} [DivInvMonoid G] [TopologicalSpace G]
    [ContinuousMul G] [ContinuousInv G] {x y : G} (h : Inseparable x y) (m : â„¤) :
    Inseparable (x ^ m) (y ^ m) :=
  (h.specializes.zpow m).antisymm (h.specializes'.zpow m)

@[to_additive]
instance : ContinuousInv (ULift G) :=
  âŸ¨continuous_uLift_up.comp (continuous_inv.comp continuous_uLift_down)âŸ©

@[to_additive]
theorem continuousOn_inv {s : Set G} : ContinuousOn Inv.inv s :=
  continuous_inv.continuousOn

@[to_additive]
theorem continuousWithinAt_inv {s : Set G} {x : G} : ContinuousWithinAt Inv.inv s x :=
  continuous_inv.continuousWithinAt

@[to_additive]
theorem continuousAt_inv {x : G} : ContinuousAt Inv.inv x :=
  continuous_inv.continuousAt

@[to_additive]
theorem tendsto_inv (a : G) : Tendsto Inv.inv (ğ“ a) (ğ“ aâ»Â¹) :=
  continuousAt_inv

/-- If a function converges to a value in a multiplicative topological group, then its inverse
converges to the inverse of this value. For the version in normed fields assuming additionally
that the limit is nonzero, use `Tendsto.inv'`. -/
@[to_additive
  "If a function converges to a value in an additive topological group, then its
  negation converges to the negation of this value."]
theorem Filter.Tendsto.inv {f : Î± â†’ G} {l : Filter Î±} {y : G} (h : Tendsto f l (ğ“ y)) :
    Tendsto (fun x => (f x)â»Â¹) l (ğ“ yâ»Â¹) :=
  (continuous_inv.tendsto y).comp h

variable [TopologicalSpace Î±] {f : Î± â†’ G} {s : Set Î±} {x : Î±}

@[to_additive (attr := continuity, fun_prop)]
theorem Continuous.inv (hf : Continuous f) : Continuous fun x => (f x)â»Â¹ :=
  continuous_inv.comp hf

@[to_additive (attr := fun_prop)]
theorem ContinuousAt.inv (hf : ContinuousAt f x) : ContinuousAt (fun x => (f x)â»Â¹) x :=
  continuousAt_inv.comp hf

@[to_additive (attr := fun_prop)]
theorem ContinuousOn.inv (hf : ContinuousOn f s) : ContinuousOn (fun x => (f x)â»Â¹) s :=
  continuous_inv.comp_continuousOn hf

@[to_additive]
theorem ContinuousWithinAt.inv (hf : ContinuousWithinAt f s x) :
    ContinuousWithinAt (fun x => (f x)â»Â¹) s x :=
  Filter.Tendsto.inv hf

@[to_additive]
instance Prod.continuousInv [TopologicalSpace H] [Inv H] [ContinuousInv H] :
    ContinuousInv (G Ã— H) :=
  âŸ¨continuous_inv.fst'.prod_mk continuous_inv.snd'âŸ©

variable {Î¹ : Type*}

@[to_additive]
instance Pi.continuousInv {C : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (C i)] [âˆ€ i, Inv (C i)]
    [âˆ€ i, ContinuousInv (C i)] : ContinuousInv (âˆ€ i, C i) where
  continuous_inv := continuous_pi fun i => (continuous_apply i).inv

/-- A version of `Pi.continuousInv` for non-dependent functions. It is needed because sometimes
Lean fails to use `Pi.continuousInv` for non-dependent functions. -/
@[to_additive
  "A version of `Pi.continuousNeg` for non-dependent functions. It is needed
  because sometimes Lean fails to use `Pi.continuousNeg` for non-dependent functions."]
instance Pi.has_continuous_inv' : ContinuousInv (Î¹ â†’ G) :=
  Pi.continuousInv

@[to_additive]
instance (priority := 100) continuousInv_of_discreteTopology [TopologicalSpace H] [Inv H]
    [DiscreteTopology H] : ContinuousInv H :=
  âŸ¨continuous_of_discreteTopologyâŸ©

section PointwiseLimits

variable (Gâ‚ Gâ‚‚ : Type*) [TopologicalSpace Gâ‚‚] [T2Space Gâ‚‚]

@[to_additive]
theorem isClosed_setOf_map_inv [Inv Gâ‚] [Inv Gâ‚‚] [ContinuousInv Gâ‚‚] :
    IsClosed { f : Gâ‚ â†’ Gâ‚‚ | âˆ€ x, f xâ»Â¹ = (f x)â»Â¹ } := by
  simp only [setOf_forall]
  exact isClosed_iInter fun i => isClosed_eq (continuous_apply _) (continuous_apply _).inv

end PointwiseLimits

instance [TopologicalSpace H] [Inv H] [ContinuousInv H] : ContinuousNeg (Additive H) where
  continuous_neg := @continuous_inv H _ _ _

instance [TopologicalSpace H] [Neg H] [ContinuousNeg H] : ContinuousInv (Multiplicative H) where
  continuous_inv := @continuous_neg H _ _ _

end ContinuousInv

section ContinuousInvolutiveInv

variable [TopologicalSpace G] [InvolutiveInv G] [ContinuousInv G] {s : Set G}

@[to_additive]
theorem IsCompact.inv (hs : IsCompact s) : IsCompact sâ»Â¹ := by
  rw [â† image_inv]
  exact hs.image continuous_inv

variable (G)

/-- Inversion in a topological group as a homeomorphism. -/
@[to_additive "Negation in a topological group as a homeomorphism."]
protected def Homeomorph.inv (G : Type*) [TopologicalSpace G] [InvolutiveInv G]
    [ContinuousInv G] : G â‰ƒâ‚œ G :=
  { Equiv.inv G with
    continuous_toFun := continuous_inv
    continuous_invFun := continuous_inv }

@[to_additive (attr := simp)]
lemma Homeomorph.coe_inv {G : Type*} [TopologicalSpace G] [InvolutiveInv G] [ContinuousInv G] :
    â‡‘(Homeomorph.inv G) = Inv.inv := rfl

@[to_additive]
theorem nhds_inv (a : G) : ğ“ aâ»Â¹ = (ğ“ a)â»Â¹ :=
  ((Homeomorph.inv G).map_nhds_eq a).symm

@[to_additive]
theorem isOpenMap_inv : IsOpenMap (Inv.inv : G â†’ G) :=
  (Homeomorph.inv _).isOpenMap

@[to_additive]
theorem isClosedMap_inv : IsClosedMap (Inv.inv : G â†’ G) :=
  (Homeomorph.inv _).isClosedMap

variable {G}

@[to_additive]
theorem IsOpen.inv (hs : IsOpen s) : IsOpen sâ»Â¹ :=
  hs.preimage continuous_inv

@[to_additive]
theorem IsClosed.inv (hs : IsClosed s) : IsClosed sâ»Â¹ :=
  hs.preimage continuous_inv

@[to_additive]
theorem inv_closure : âˆ€ s : Set G, (closure s)â»Â¹ = closure sâ»Â¹ :=
  (Homeomorph.inv G).preimage_closure

variable [TopologicalSpace Î±] {f : Î± â†’ G} {s : Set Î±} {x : Î±}

@[to_additive (attr := simp)]
lemma continuous_inv_iff : Continuous fâ»Â¹ â†” Continuous f := (Homeomorph.inv G).comp_continuous_iff

@[to_additive (attr := simp)]
lemma continuousAt_inv_iff : ContinuousAt fâ»Â¹ x â†” ContinuousAt f x :=
  (Homeomorph.inv G).comp_continuousAt_iff _ _

@[to_additive (attr := simp)]
lemma continuousOn_inv_iff : ContinuousOn fâ»Â¹ s â†” ContinuousOn f s :=
  (Homeomorph.inv G).comp_continuousOn_iff _ _

@[to_additive] alias âŸ¨Continuous.of_inv, _âŸ© := continuous_inv_iff
@[to_additive] alias âŸ¨ContinuousAt.of_inv, _âŸ© := continuousAt_inv_iff
@[to_additive] alias âŸ¨ContinuousOn.of_inv, _âŸ© := continuousOn_inv_iff

end ContinuousInvolutiveInv

section LatticeOps

variable {Î¹' : Sort*} [Inv G]

@[to_additive]
theorem continuousInv_sInf {ts : Set (TopologicalSpace G)}
    (h : âˆ€ t âˆˆ ts, @ContinuousInv G t _) : @ContinuousInv G (sInf ts) _ :=
  letI := sInf ts
  { continuous_inv :=
      continuous_sInf_rng.2 fun t ht =>
        continuous_sInf_dom ht (@ContinuousInv.continuous_inv G t _ (h t ht)) }

@[to_additive]
theorem continuousInv_iInf {ts' : Î¹' â†’ TopologicalSpace G}
    (h' : âˆ€ i, @ContinuousInv G (ts' i) _) : @ContinuousInv G (â¨… i, ts' i) _ := by
  rw [â† sInf_range]
  exact continuousInv_sInf (Set.forall_mem_range.mpr h')

@[to_additive]
theorem continuousInv_inf {tâ‚ tâ‚‚ : TopologicalSpace G} (hâ‚ : @ContinuousInv G tâ‚ _)
    (hâ‚‚ : @ContinuousInv G tâ‚‚ _) : @ContinuousInv G (tâ‚ âŠ“ tâ‚‚) _ := by
  rw [inf_eq_iInf]
  refine continuousInv_iInf fun b => ?_
  cases b <;> assumption

end LatticeOps

@[to_additive]
theorem Inducing.continuousInv {G H : Type*} [Inv G] [Inv H] [TopologicalSpace G]
    [TopologicalSpace H] [ContinuousInv H] {f : G â†’ H} (hf : Inducing f)
    (hf_inv : âˆ€ x, f xâ»Â¹ = (f x)â»Â¹) : ContinuousInv G :=
  âŸ¨hf.continuous_iff.2 <| by simpa only [Function.comp_def, hf_inv] using hf.continuous.invâŸ©

section TopologicalGroup

/-!
### Topological groups

A topological group is a group in which the multiplication and inversion operations are
continuous. Topological additive groups are defined in the same way. Equivalently, we can require
that the division operation `x y â†¦ x * yâ»Â¹` (resp., subtraction) is continuous.
-/

-- Porting note (#11215): TODO should this docstring be extended
-- to match the multiplicative version?
/-- A topological (additive) group is a group in which the addition and negation operations are
continuous. -/
class TopologicalAddGroup (G : Type u) [TopologicalSpace G] [AddGroup G] extends
  ContinuousAdd G, ContinuousNeg G : Prop

/-- A topological group is a group in which the multiplication and inversion operations are
continuous.

When you declare an instance that does not already have a `UniformSpace` instance,
you should also provide an instance of `UniformSpace` and `UniformGroup` using
`TopologicalGroup.toUniformSpace` and `topologicalCommGroup_isUniform`. -/
-- Porting note: check that these â†‘ names exist once they've been ported in the future.
@[to_additive]
class TopologicalGroup (G : Type*) [TopologicalSpace G] [Group G] extends ContinuousMul G,
  ContinuousInv G : Prop

section Conj

instance ConjAct.units_continuousConstSMul {M} [Monoid M] [TopologicalSpace M]
    [ContinuousMul M] : ContinuousConstSMul (ConjAct MË£) M :=
  âŸ¨fun _ => (continuous_const.mul continuous_id).mul continuous_constâŸ©

variable [TopologicalSpace G] [Inv G] [Mul G] [ContinuousMul G]

/-- Conjugation is jointly continuous on `G Ã— G` when both `mul` and `inv` are continuous. -/
@[to_additive
  "Conjugation is jointly continuous on `G Ã— G` when both `add` and `neg` are continuous."]
theorem TopologicalGroup.continuous_conj_prod [ContinuousInv G] :
    Continuous fun g : G Ã— G => g.fst * g.snd * g.fstâ»Â¹ :=
  continuous_mul.mul (continuous_inv.comp continuous_fst)

/-- Conjugation by a fixed element is continuous when `mul` is continuous. -/
@[to_additive (attr := continuity)
  "Conjugation by a fixed element is continuous when `add` is continuous."]
theorem TopologicalGroup.continuous_conj (g : G) : Continuous fun h : G => g * h * gâ»Â¹ :=
  (continuous_mul_right gâ»Â¹).comp (continuous_mul_left g)

/-- Conjugation acting on fixed element of the group is continuous when both `mul` and
`inv` are continuous. -/
@[to_additive (attr := continuity)
  "Conjugation acting on fixed element of the additive group is continuous when both
    `add` and `neg` are continuous."]
theorem TopologicalGroup.continuous_conj' [ContinuousInv G] (h : G) :
    Continuous fun g : G => g * h * gâ»Â¹ :=
  (continuous_mul_right h).mul continuous_inv

end Conj

variable [TopologicalSpace G] [Group G] [TopologicalGroup G] [TopologicalSpace Î±] {f : Î± â†’ G}
  {s : Set Î±} {x : Î±}

instance : TopologicalGroup (ULift G) where

section ZPow

@[to_additive (attr := continuity)]
theorem continuous_zpow : âˆ€ z : â„¤, Continuous fun a : G => a ^ z
  | Int.ofNat n => by simpa using continuous_pow n
  | Int.negSucc n => by simpa using (continuous_pow (n + 1)).inv

instance AddGroup.continuousConstSMul_int {A} [AddGroup A] [TopologicalSpace A]
    [TopologicalAddGroup A] : ContinuousConstSMul â„¤ A :=
  âŸ¨continuous_zsmulâŸ©

instance AddGroup.continuousSMul_int {A} [AddGroup A] [TopologicalSpace A]
    [TopologicalAddGroup A] : ContinuousSMul â„¤ A :=
  âŸ¨continuous_prod_of_discrete_left.mpr continuous_zsmulâŸ©

@[to_additive (attr := continuity, fun_prop)]
theorem Continuous.zpow {f : Î± â†’ G} (h : Continuous f) (z : â„¤) : Continuous fun b => f b ^ z :=
  (continuous_zpow z).comp h

@[to_additive]
theorem continuousOn_zpow {s : Set G} (z : â„¤) : ContinuousOn (fun x => x ^ z) s :=
  (continuous_zpow z).continuousOn

@[to_additive]
theorem continuousAt_zpow (x : G) (z : â„¤) : ContinuousAt (fun x => x ^ z) x :=
  (continuous_zpow z).continuousAt

@[to_additive]
theorem Filter.Tendsto.zpow {Î±} {l : Filter Î±} {f : Î± â†’ G} {x : G} (hf : Tendsto f l (ğ“ x))
    (z : â„¤) : Tendsto (fun x => f x ^ z) l (ğ“ (x ^ z)) :=
  (continuousAt_zpow _ _).tendsto.comp hf

@[to_additive]
theorem ContinuousWithinAt.zpow {f : Î± â†’ G} {x : Î±} {s : Set Î±} (hf : ContinuousWithinAt f s x)
    (z : â„¤) : ContinuousWithinAt (fun x => f x ^ z) s x :=
  Filter.Tendsto.zpow hf z

@[to_additive (attr := fun_prop)]
theorem ContinuousAt.zpow {f : Î± â†’ G} {x : Î±} (hf : ContinuousAt f x) (z : â„¤) :
    ContinuousAt (fun x => f x ^ z) x :=
  Filter.Tendsto.zpow hf z

@[to_additive (attr := fun_prop)]
theorem ContinuousOn.zpow {f : Î± â†’ G} {s : Set Î±} (hf : ContinuousOn f s) (z : â„¤) :
    ContinuousOn (fun x => f x ^ z) s := fun x hx => (hf x hx).zpow z

end ZPow

section OrderedCommGroup

variable [TopologicalSpace H] [OrderedCommGroup H] [ContinuousInv H]

@[to_additive]
theorem tendsto_inv_nhdsWithin_Ioi {a : H} : Tendsto Inv.inv (ğ“[>] a) (ğ“[<] aâ»Â¹) :=
  (continuous_inv.tendsto a).inf <| by simp [tendsto_principal_principal]

@[to_additive]
theorem tendsto_inv_nhdsWithin_Iio {a : H} : Tendsto Inv.inv (ğ“[<] a) (ğ“[>] aâ»Â¹) :=
  (continuous_inv.tendsto a).inf <| by simp [tendsto_principal_principal]

@[to_additive]
theorem tendsto_inv_nhdsWithin_Ioi_inv {a : H} : Tendsto Inv.inv (ğ“[>] aâ»Â¹) (ğ“[<] a) := by
  simpa only [inv_inv] using @tendsto_inv_nhdsWithin_Ioi _ _ _ _ aâ»Â¹

@[to_additive]
theorem tendsto_inv_nhdsWithin_Iio_inv {a : H} : Tendsto Inv.inv (ğ“[<] aâ»Â¹) (ğ“[>] a) := by
  simpa only [inv_inv] using @tendsto_inv_nhdsWithin_Iio _ _ _ _ aâ»Â¹

@[to_additive]
theorem tendsto_inv_nhdsWithin_Ici {a : H} : Tendsto Inv.inv (ğ“[â‰¥] a) (ğ“[â‰¤] aâ»Â¹) :=
  (continuous_inv.tendsto a).inf <| by simp [tendsto_principal_principal]

@[to_additive]
theorem tendsto_inv_nhdsWithin_Iic {a : H} : Tendsto Inv.inv (ğ“[â‰¤] a) (ğ“[â‰¥] aâ»Â¹) :=
  (continuous_inv.tendsto a).inf <| by simp [tendsto_principal_principal]

@[to_additive]
theorem tendsto_inv_nhdsWithin_Ici_inv {a : H} : Tendsto Inv.inv (ğ“[â‰¥] aâ»Â¹) (ğ“[â‰¤] a) := by
  simpa only [inv_inv] using @tendsto_inv_nhdsWithin_Ici _ _ _ _ aâ»Â¹

@[to_additive]
theorem tendsto_inv_nhdsWithin_Iic_inv {a : H} : Tendsto Inv.inv (ğ“[â‰¤] aâ»Â¹) (ğ“[â‰¥] a) := by
  simpa only [inv_inv] using @tendsto_inv_nhdsWithin_Iic _ _ _ _ aâ»Â¹

end OrderedCommGroup

@[to_additive]
instance [TopologicalSpace H] [Group H] [TopologicalGroup H] : TopologicalGroup (G Ã— H) where
  continuous_inv := continuous_inv.prod_map continuous_inv

@[to_additive]
instance Pi.topologicalGroup {C : Î² â†’ Type*} [âˆ€ b, TopologicalSpace (C b)] [âˆ€ b, Group (C b)]
    [âˆ€ b, TopologicalGroup (C b)] : TopologicalGroup (âˆ€ b, C b) where
  continuous_inv := continuous_pi fun i => (continuous_apply i).inv

open MulOpposite

@[to_additive]
instance [Inv Î±] [ContinuousInv Î±] : ContinuousInv Î±áµáµ’áµ– :=
  opHomeomorph.symm.inducing.continuousInv unop_inv

/-- If multiplication is continuous in `Î±`, then it also is in `Î±áµáµ’áµ–`. -/
@[to_additive "If addition is continuous in `Î±`, then it also is in `Î±áµƒáµ’áµ–`."]
instance [Group Î±] [TopologicalGroup Î±] : TopologicalGroup Î±áµáµ’áµ– where

variable (G)

@[to_additive]
theorem nhds_one_symm : comap Inv.inv (ğ“ (1 : G)) = ğ“ (1 : G) :=
  ((Homeomorph.inv G).comap_nhds_eq _).trans (congr_arg nhds inv_one)

@[to_additive]
theorem nhds_one_symm' : map Inv.inv (ğ“ (1 : G)) = ğ“ (1 : G) :=
  ((Homeomorph.inv G).map_nhds_eq _).trans (congr_arg nhds inv_one)

@[to_additive]
theorem inv_mem_nhds_one {S : Set G} (hS : S âˆˆ (ğ“ 1 : Filter G)) : Sâ»Â¹ âˆˆ ğ“ (1 : G) := by
  rwa [â† nhds_one_symm'] at hS

/-- The map `(x, y) â†¦ (x, x * y)` as a homeomorphism. This is a shear mapping. -/
@[to_additive "The map `(x, y) â†¦ (x, x + y)` as a homeomorphism. This is a shear mapping."]
protected def Homeomorph.shearMulRight : G Ã— G â‰ƒâ‚œ G Ã— G :=
  { Equiv.prodShear (Equiv.refl _) Equiv.mulLeft with
    continuous_toFun := continuous_fst.prod_mk continuous_mul
    continuous_invFun := continuous_fst.prod_mk <| continuous_fst.inv.mul continuous_snd }

@[to_additive (attr := simp)]
theorem Homeomorph.shearMulRight_coe :
    â‡‘(Homeomorph.shearMulRight G) = fun z : G Ã— G => (z.1, z.1 * z.2) :=
  rfl

@[to_additive (attr := simp)]
theorem Homeomorph.shearMulRight_symm_coe :
    â‡‘(Homeomorph.shearMulRight G).symm = fun z : G Ã— G => (z.1, z.1â»Â¹ * z.2) :=
  rfl

variable {G}

@[to_additive]
protected theorem Inducing.topologicalGroup {F : Type*} [Group H] [TopologicalSpace H]
    [FunLike F H G] [MonoidHomClass F H G] (f : F) (hf : Inducing f) : TopologicalGroup H :=
  { toContinuousMul := hf.continuousMul _
    toContinuousInv := hf.continuousInv (map_inv f) }

@[to_additive]
-- Porting note: removed `protected` (needs to be in namespace)
theorem topologicalGroup_induced {F : Type*} [Group H] [FunLike F H G] [MonoidHomClass F H G]
    (f : F) :
    @TopologicalGroup H (induced f â€¹_â€º) _ :=
  letI := induced f â€¹_â€º
  Inducing.topologicalGroup f âŸ¨rflâŸ©

namespace Subgroup

@[to_additive]
instance (S : Subgroup G) : TopologicalGroup S :=
  Inducing.topologicalGroup S.subtype inducing_subtype_val

end Subgroup

/-- The (topological-space) closure of a subgroup of a topological group is
itself a subgroup. -/
@[to_additive
  "The (topological-space) closure of an additive subgroup of an additive topological group is
  itself an additive subgroup."]
def Subgroup.topologicalClosure (s : Subgroup G) : Subgroup G :=
  { s.toSubmonoid.topologicalClosure with
    carrier := _root_.closure (s : Set G)
    inv_mem' := fun {g} hg => by simpa only [â† Set.mem_inv, inv_closure, inv_coe_set] using hg }

@[to_additive (attr := simp)]
theorem Subgroup.topologicalClosure_coe {s : Subgroup G} :
    (s.topologicalClosure : Set G) = _root_.closure s :=
  rfl

@[to_additive]
theorem Subgroup.le_topologicalClosure (s : Subgroup G) : s â‰¤ s.topologicalClosure :=
  _root_.subset_closure

@[to_additive]
theorem Subgroup.isClosed_topologicalClosure (s : Subgroup G) :
    IsClosed (s.topologicalClosure : Set G) := isClosed_closure

@[to_additive]
theorem Subgroup.topologicalClosure_minimal (s : Subgroup G) {t : Subgroup G} (h : s â‰¤ t)
    (ht : IsClosed (t : Set G)) : s.topologicalClosure â‰¤ t :=
  closure_minimal h ht

@[to_additive]
theorem DenseRange.topologicalClosure_map_subgroup [Group H] [TopologicalSpace H]
    [TopologicalGroup H] {f : G â†’* H} (hf : Continuous f) (hf' : DenseRange f) {s : Subgroup G}
    (hs : s.topologicalClosure = âŠ¤) : (s.map f).topologicalClosure = âŠ¤ := by
  rw [SetLike.ext'_iff] at hs âŠ¢
  simp only [Subgroup.topologicalClosure_coe, Subgroup.coe_top, â† dense_iff_closure_eq] at hs âŠ¢
  exact hf'.dense_image hf hs

/-- The topological closure of a normal subgroup is normal. -/
@[to_additive "The topological closure of a normal additive subgroup is normal."]
theorem Subgroup.is_normal_topologicalClosure {G : Type*} [TopologicalSpace G] [Group G]
    [TopologicalGroup G] (N : Subgroup G) [N.Normal] : (Subgroup.topologicalClosure N).Normal where
  conj_mem n hn g := by
    apply map_mem_closure (TopologicalGroup.continuous_conj g) hn
    exact fun m hm => Subgroup.Normal.conj_mem inferInstance m hm g

@[to_additive]
theorem mul_mem_connectedComponent_one {G : Type*} [TopologicalSpace G] [MulOneClass G]
    [ContinuousMul G] {g h : G} (hg : g âˆˆ connectedComponent (1 : G))
    (hh : h âˆˆ connectedComponent (1 : G)) : g * h âˆˆ connectedComponent (1 : G) := by
  rw [connectedComponent_eq hg]
  have hmul : g âˆˆ connectedComponent (g * h) := by
    apply Continuous.image_connectedComponent_subset (continuous_mul_left g)
    rw [â† connectedComponent_eq hh]
    exact âŸ¨(1 : G), mem_connectedComponent, by simp only [mul_one]âŸ©
  simpa [â† connectedComponent_eq hmul] using mem_connectedComponent

@[to_additive]
theorem inv_mem_connectedComponent_one {G : Type*} [TopologicalSpace G] [Group G]
    [TopologicalGroup G] {g : G} (hg : g âˆˆ connectedComponent (1 : G)) :
    gâ»Â¹ âˆˆ connectedComponent (1 : G) := by
  rw [â† inv_one]
  exact
    Continuous.image_connectedComponent_subset continuous_inv _
      ((Set.mem_image _ _ _).mp âŸ¨g, hg, rflâŸ©)

/-- The connected component of 1 is a subgroup of `G`. -/
@[to_additive "The connected component of 0 is a subgroup of `G`."]
def Subgroup.connectedComponentOfOne (G : Type*) [TopologicalSpace G] [Group G]
    [TopologicalGroup G] : Subgroup G where
  carrier := connectedComponent (1 : G)
  one_mem' := mem_connectedComponent
  mul_mem' hg hh := mul_mem_connectedComponent_one hg hh
  inv_mem' hg := inv_mem_connectedComponent_one hg

/-- If a subgroup of a topological group is commutative, then so is its topological closure. -/
@[to_additive
  "If a subgroup of an additive topological group is commutative, then so is its
  topological closure."]
def Subgroup.commGroupTopologicalClosure [T2Space G] (s : Subgroup G)
    (hs : âˆ€ x y : s, x * y = y * x) : CommGroup s.topologicalClosure :=
  { s.topologicalClosure.toGroup, s.toSubmonoid.commMonoidTopologicalClosure hs with }

variable (G) in
@[to_additive]
lemma Subgroup.coe_topologicalClosure_bot :
    ((âŠ¥ : Subgroup G).topologicalClosure : Set G) = _root_.closure ({1} : Set G) := by simp

@[to_additive exists_nhds_half_neg]
theorem exists_nhds_split_inv {s : Set G} (hs : s âˆˆ ğ“ (1 : G)) :
    âˆƒ V âˆˆ ğ“ (1 : G), âˆ€ v âˆˆ V, âˆ€ w âˆˆ V, v / w âˆˆ s := by
  have : (fun p : G Ã— G => p.1 * p.2â»Â¹) â»Â¹' s âˆˆ ğ“ ((1, 1) : G Ã— G) :=
    continuousAt_fst.mul continuousAt_snd.inv (by simpa)
  simpa only [div_eq_mul_inv, nhds_prod_eq, mem_prod_self_iff, prod_subset_iff, mem_preimage] using
    this

@[to_additive]
theorem nhds_translation_mul_inv (x : G) : comap (Â· * xâ»Â¹) (ğ“ 1) = ğ“ x :=
  ((Homeomorph.mulRight xâ»Â¹).comap_nhds_eq 1).trans <| show ğ“ (1 * xâ»Â¹â»Â¹) = ğ“ x by simp

@[to_additive (attr := simp)]
theorem map_mul_left_nhds (x y : G) : map (x * Â·) (ğ“ y) = ğ“ (x * y) :=
  (Homeomorph.mulLeft x).map_nhds_eq y

@[to_additive]
theorem map_mul_left_nhds_one (x : G) : map (x * Â·) (ğ“ 1) = ğ“ x := by simp

@[to_additive (attr := simp)]
theorem map_mul_right_nhds (x y : G) : map (Â· * x) (ğ“ y) = ğ“ (y * x) :=
  (Homeomorph.mulRight x).map_nhds_eq y

@[to_additive]
theorem map_mul_right_nhds_one (x : G) : map (Â· * x) (ğ“ 1) = ğ“ x := by simp

@[to_additive]
theorem Filter.HasBasis.nhds_of_one {Î¹ : Sort*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set G}
    (hb : HasBasis (ğ“ 1 : Filter G) p s) (x : G) :
    HasBasis (ğ“ x) p fun i => { y | y / x âˆˆ s i } := by
  rw [â† nhds_translation_mul_inv]
  simp_rw [div_eq_mul_inv]
  exact hb.comap _

@[to_additive]
theorem mem_closure_iff_nhds_one {x : G} {s : Set G} :
    x âˆˆ closure s â†” âˆ€ U âˆˆ (ğ“ 1 : Filter G), âˆƒ y âˆˆ s, y / x âˆˆ U := by
  rw [mem_closure_iff_nhds_basis ((ğ“ 1 : Filter G).basis_sets.nhds_of_one x)]
  simp_rw [Set.mem_setOf, id]

/-- A monoid homomorphism (a bundled morphism of a type that implements `MonoidHomClass`) from a
topological group to a topological monoid is continuous provided that it is continuous at one. See
also `uniformContinuous_of_continuousAt_one`. -/
@[to_additive
  "An additive monoid homomorphism (a bundled morphism of a type that implements
  `AddMonoidHomClass`) from an additive topological group to an additive topological monoid is
  continuous provided that it is continuous at zero. See also
  `uniformContinuous_of_continuousAt_zero`."]
theorem continuous_of_continuousAt_one {M hom : Type*} [MulOneClass M] [TopologicalSpace M]
    [ContinuousMul M] [FunLike hom G M] [MonoidHomClass hom G M] (f : hom)
    (hf : ContinuousAt f 1) :
    Continuous f :=
  continuous_iff_continuousAt.2 fun x => by
    simpa only [ContinuousAt, â† map_mul_left_nhds_one x, tendsto_map'_iff, Function.comp_def,
      map_mul, map_one, mul_one] using hf.tendsto.const_mul (f x)

@[to_additive continuous_of_continuousAt_zeroâ‚‚]
theorem continuous_of_continuousAt_oneâ‚‚ {H M : Type*} [CommMonoid M] [TopologicalSpace M]
    [ContinuousMul M] [Group H] [TopologicalSpace H] [TopologicalGroup H] (f : G â†’* H â†’* M)
    (hf : ContinuousAt (fun x : G Ã— H â†¦ f x.1 x.2) (1, 1))
    (hl : âˆ€ x, ContinuousAt (f x) 1) (hr : âˆ€ y, ContinuousAt (f Â· y) 1) :
    Continuous (fun x : G Ã— H â†¦ f x.1 x.2) := continuous_iff_continuousAt.2 fun (x, y) => by
  simp only [ContinuousAt, nhds_prod_eq, â† map_mul_left_nhds_one x, â† map_mul_left_nhds_one y,
    prod_map_map_eq, tendsto_map'_iff, Function.comp_def, map_mul, MonoidHom.mul_apply] at *
  refine ((tendsto_const_nhds.mul ((hr y).comp tendsto_fst)).mul
    (((hl x).comp tendsto_snd).mul hf)).mono_right (le_of_eq ?_)
  simp only [map_one, mul_one, MonoidHom.one_apply]

@[to_additive]
theorem TopologicalGroup.ext {G : Type*} [Group G] {t t' : TopologicalSpace G}
    (tg : @TopologicalGroup G t _) (tg' : @TopologicalGroup G t' _)
    (h : @nhds G t 1 = @nhds G t' 1) : t = t' :=
  TopologicalSpace.ext_nhds fun x â†¦ by
    rw [â† @nhds_translation_mul_inv G t _ _ x, â† @nhds_translation_mul_inv G t' _ _ x, â† h]

@[to_additive]
theorem TopologicalGroup.ext_iff {G : Type*} [Group G] {t t' : TopologicalSpace G}
    (tg : @TopologicalGroup G t _) (tg' : @TopologicalGroup G t' _) :
    t = t' â†” @nhds G t 1 = @nhds G t' 1 :=
  âŸ¨fun h => h â–¸ rfl, tg.ext tg'âŸ©

@[to_additive]
theorem ContinuousInv.of_nhds_one {G : Type*} [Group G] [TopologicalSpace G]
    (hinv : Tendsto (fun x : G => xâ»Â¹) (ğ“ 1) (ğ“ 1))
    (hleft : âˆ€ xâ‚€ : G, ğ“ xâ‚€ = map (fun x : G => xâ‚€ * x) (ğ“ 1))
    (hconj : âˆ€ xâ‚€ : G, Tendsto (fun x : G => xâ‚€ * x * xâ‚€â»Â¹) (ğ“ 1) (ğ“ 1)) : ContinuousInv G := by
  refine âŸ¨continuous_iff_continuousAt.2 fun xâ‚€ => ?_âŸ©
  have : Tendsto (fun x => xâ‚€â»Â¹ * (xâ‚€ * xâ»Â¹ * xâ‚€â»Â¹)) (ğ“ 1) (map (xâ‚€â»Â¹ * Â·) (ğ“ 1)) :=
    (tendsto_map.comp <| hconj xâ‚€).comp hinv
  simpa only [ContinuousAt, hleft xâ‚€, hleft xâ‚€â»Â¹, tendsto_map'_iff, Function.comp_def, mul_assoc,
    mul_inv_rev, inv_mul_cancel_left] using this

@[to_additive]
theorem TopologicalGroup.of_nhds_one' {G : Type u} [Group G] [TopologicalSpace G]
    (hmul : Tendsto (uncurry ((Â· * Â·) : G â†’ G â†’ G)) (ğ“ 1 Ã—Ë¢ ğ“ 1) (ğ“ 1))
    (hinv : Tendsto (fun x : G => xâ»Â¹) (ğ“ 1) (ğ“ 1))
    (hleft : âˆ€ xâ‚€ : G, ğ“ xâ‚€ = map (fun x => xâ‚€ * x) (ğ“ 1))
    (hright : âˆ€ xâ‚€ : G, ğ“ xâ‚€ = map (fun x => x * xâ‚€) (ğ“ 1)) : TopologicalGroup G :=
  { toContinuousMul := ContinuousMul.of_nhds_one hmul hleft hright
    toContinuousInv :=
      ContinuousInv.of_nhds_one hinv hleft fun xâ‚€ =>
        le_of_eq
          (by
            rw [show (fun x => xâ‚€ * x * xâ‚€â»Â¹) = (fun x => x * xâ‚€â»Â¹) âˆ˜ fun x => xâ‚€ * x from rfl, â†
              map_map, â† hleft, hright, map_map]
            simp [(Â· âˆ˜ Â·)]) }

@[to_additive]
theorem TopologicalGroup.of_nhds_one {G : Type u} [Group G] [TopologicalSpace G]
    (hmul : Tendsto (uncurry ((Â· * Â·) : G â†’ G â†’ G)) (ğ“ 1 Ã—Ë¢ ğ“ 1) (ğ“ 1))
    (hinv : Tendsto (fun x : G => xâ»Â¹) (ğ“ 1) (ğ“ 1))
    (hleft : âˆ€ xâ‚€ : G, ğ“ xâ‚€ = map (xâ‚€ * Â·) (ğ“ 1))
    (hconj : âˆ€ xâ‚€ : G, Tendsto (xâ‚€ * Â· * xâ‚€â»Â¹) (ğ“ 1) (ğ“ 1)) : TopologicalGroup G := by
  refine TopologicalGroup.of_nhds_one' hmul hinv hleft fun xâ‚€ => ?_
  replace hconj : âˆ€ xâ‚€ : G, map (xâ‚€ * Â· * xâ‚€â»Â¹) (ğ“ 1) = ğ“ 1 :=
    fun xâ‚€ => map_eq_of_inverse (xâ‚€â»Â¹ * Â· * xâ‚€â»Â¹â»Â¹) (by ext; simp [mul_assoc]) (hconj _) (hconj _)
  rw [â† hconj xâ‚€]
  simpa [Function.comp_def] using hleft _

@[to_additive]
theorem TopologicalGroup.of_comm_of_nhds_one {G : Type u} [CommGroup G] [TopologicalSpace G]
    (hmul : Tendsto (uncurry ((Â· * Â·) : G â†’ G â†’ G)) (ğ“ 1 Ã—Ë¢ ğ“ 1) (ğ“ 1))
    (hinv : Tendsto (fun x : G => xâ»Â¹) (ğ“ 1) (ğ“ 1))
    (hleft : âˆ€ xâ‚€ : G, ğ“ xâ‚€ = map (xâ‚€ * Â·) (ğ“ 1)) : TopologicalGroup G :=
  TopologicalGroup.of_nhds_one hmul hinv hleft (by simpa using tendsto_id)

variable (G) in
/-- Any first countable topological group has an antitone neighborhood basis `u : â„• â†’ Set G` for
which `(u (n + 1)) ^ 2 âŠ† u n`. The existence of such a neighborhood basis is a key tool for
`QuotientGroup.completeSpace` -/
@[to_additive
  "Any first countable topological additive group has an antitone neighborhood basis
  `u : â„• â†’ set G` for which `u (n + 1) + u (n + 1) âŠ† u n`.
  The existence of such a neighborhood basis is a key tool for `QuotientAddGroup.completeSpace`"]
theorem TopologicalGroup.exists_antitone_basis_nhds_one [FirstCountableTopology G] :
    âˆƒ u : â„• â†’ Set G, (ğ“ 1).HasAntitoneBasis u âˆ§ âˆ€ n, u (n + 1) * u (n + 1) âŠ† u n := by
  rcases (ğ“ (1 : G)).exists_antitone_basis with âŸ¨u, hu, u_antiâŸ©
  have :=
    ((hu.prod_nhds hu).tendsto_iff hu).mp
      (by simpa only [mul_one] using continuous_mul.tendsto ((1, 1) : G Ã— G))
  simp only [and_self_iff, mem_prod, and_imp, Prod.forall, exists_true_left, Prod.exists,
    forall_true_left] at this
  have event_mul : âˆ€ n : â„•, âˆ€á¶  m in atTop, u m * u m âŠ† u n := by
    intro n
    rcases this n with âŸ¨j, k, -, hâŸ©
    refine atTop_basis.eventually_iff.mpr âŸ¨max j k, True.intro, fun m hm => ?_âŸ©
    rintro - âŸ¨a, ha, b, hb, rflâŸ©
    exact h a b (u_anti ((le_max_left _ _).trans hm) ha) (u_anti ((le_max_right _ _).trans hm) hb)
  obtain âŸ¨Ï†, -, hÏ†, Ï†_anti_basisâŸ© := HasAntitoneBasis.subbasis_with_rel âŸ¨hu, u_antiâŸ© event_mul
  exact âŸ¨u âˆ˜ Ï†, Ï†_anti_basis, fun n => hÏ† n.lt_succ_selfâŸ©

end TopologicalGroup

namespace QuotientGroup

variable [TopologicalSpace G] [Group G]

@[to_additive]
instance instTopologicalSpace (N : Subgroup G) : TopologicalSpace (G â§¸ N) :=
  instTopologicalSpaceQuotient

@[to_additive]
theorem quotientMap_mk (N : Subgroup G) : QuotientMap (mk : G â†’ G â§¸ N) :=
  quotientMap_quot_mk

variable [TopologicalGroup G] (N : Subgroup G)

@[to_additive]
theorem isOpenMap_coe : IsOpenMap ((â†‘) : G â†’ G â§¸ N) :=
  isOpenMap_quotient_mk'_mul

@[to_additive (attr := simp)]
theorem dense_preimage_mk {s : Set (G â§¸ N)} : Dense ((â†‘) â»Â¹' s : Set G) â†” Dense s :=
  letI := leftRel N -- `Dense.quotient` assumes `[Setoid G]`
  âŸ¨fun h â†¦ h.quotient.mono <| image_preimage_subset _ _, fun h â†¦ h.preimage <| isOpenMap_coe _âŸ©

@[to_additive]
theorem dense_image_mk {s : Set G} :
    Dense (mk '' s : Set (G â§¸ N)) â†” Dense (s * (N : Set G)) := by
  rw [â† dense_preimage_mk, preimage_image_mk_eq_mul]

@[to_additive]
instance instTopologicalGroup [N.Normal] : TopologicalGroup (G â§¸ N) where
  continuous_mul := by
    have cont : Continuous (((â†‘) : G â†’ G â§¸ N) âˆ˜ fun p : G Ã— G â†¦ p.fst * p.snd) :=
      continuous_quot_mk.comp continuous_mul
    have quot : QuotientMap fun p : G Ã— G â†¦ ((p.1 : G â§¸ N), (p.2 : G â§¸ N)) := by
      apply IsOpenMap.to_quotientMap
      Â· exact (QuotientGroup.isOpenMap_coe N).prod (QuotientGroup.isOpenMap_coe N)
      Â· exact continuous_quot_mk.prod_map continuous_quot_mk
      Â· exact (surjective_quot_mk _).prodMap (surjective_quot_mk _)
    exact quot.continuous_iff.2 cont
  continuous_inv := continuous_inv.quotient_map' _

@[to_additive (attr := deprecated (since := "2024-08-05"))]
theorem _root_.topologicalGroup_quotient [N.Normal] : TopologicalGroup (G â§¸ N) :=
  instTopologicalGroup N

/-- Neighborhoods in the quotient are precisely the map of neighborhoods in the prequotient. -/
@[to_additive
  "Neighborhoods in the quotient are precisely the map of neighborhoods in the prequotient."]
theorem nhds_eq (x : G) : ğ“ (x : G â§¸ N) = Filter.map (â†‘) (ğ“ x) :=
  le_antisymm ((QuotientGroup.isOpenMap_coe N).nhds_le x) continuous_quot_mk.continuousAt

@[to_additive]
instance instFirstCountableTopology [FirstCountableTopology G] :
    FirstCountableTopology (G â§¸ N) where
  nhds_generated_countable := mk_surjective.forall.2 fun x â†¦ nhds_eq N x â–¸ inferInstance

@[to_additive (attr := deprecated (since := "2024-08-05"))]
theorem nhds_one_isCountablyGenerated [FirstCountableTopology G] [N.Normal] :
    (ğ“ (1 : G â§¸ N)).IsCountablyGenerated :=
  inferInstance

end QuotientGroup

/-- A typeclass saying that `p : G Ã— G â†¦ p.1 - p.2` is a continuous function. This property
automatically holds for topological additive groups but it also holds, e.g., for `â„â‰¥0`. -/
class ContinuousSub (G : Type*) [TopologicalSpace G] [Sub G] : Prop where
  continuous_sub : Continuous fun p : G Ã— G => p.1 - p.2

/-- A typeclass saying that `p : G Ã— G â†¦ p.1 / p.2` is a continuous function. This property
automatically holds for topological groups. Lemmas using this class have primes.
The unprimed version is for `GroupWithZero`. -/
@[to_additive existing]
class ContinuousDiv (G : Type*) [TopologicalSpace G] [Div G] : Prop where
  continuous_div' : Continuous fun p : G Ã— G => p.1 / p.2

-- see Note [lower instance priority]
@[to_additive]
instance (priority := 100) TopologicalGroup.to_continuousDiv [TopologicalSpace G] [Group G]
    [TopologicalGroup G] : ContinuousDiv G :=
  âŸ¨by
    simp only [div_eq_mul_inv]
    exact continuous_fst.mul continuous_snd.invâŸ©

export ContinuousSub (continuous_sub)

export ContinuousDiv (continuous_div')

section ContinuousDiv

variable [TopologicalSpace G] [Div G] [ContinuousDiv G]

@[to_additive sub]
theorem Filter.Tendsto.div' {f g : Î± â†’ G} {l : Filter Î±} {a b : G} (hf : Tendsto f l (ğ“ a))
    (hg : Tendsto g l (ğ“ b)) : Tendsto (fun x => f x / g x) l (ğ“ (a / b)) :=
  (continuous_div'.tendsto (a, b)).comp (hf.prod_mk_nhds hg)

@[to_additive const_sub]
theorem Filter.Tendsto.const_div' (b : G) {c : G} {f : Î± â†’ G} {l : Filter Î±}
    (h : Tendsto f l (ğ“ c)) : Tendsto (fun k : Î± => b / f k) l (ğ“ (b / c)) :=
  tendsto_const_nhds.div' h

@[to_additive]
lemma Filter.tendsto_const_div_iff {G : Type*} [CommGroup G] [TopologicalSpace G] [ContinuousDiv G]
    (b : G) {c : G} {f : Î± â†’ G} {l : Filter Î±} :
    Tendsto (fun k : Î± â†¦ b / f k) l (ğ“ (b / c)) â†” Tendsto f l (ğ“ c) := by
  refine âŸ¨fun h â†¦ ?_, Filter.Tendsto.const_div' bâŸ©
  convert h.const_div' b with k <;> rw [div_div_cancel]

@[to_additive sub_const]
theorem Filter.Tendsto.div_const' {c : G} {f : Î± â†’ G} {l : Filter Î±} (h : Tendsto f l (ğ“ c))
    (b : G) : Tendsto (f Â· / b) l (ğ“ (c / b)) :=
  h.div' tendsto_const_nhds

lemma Filter.tendsto_div_const_iff {G : Type*}
    [CommGroupWithZero G] [TopologicalSpace G] [ContinuousDiv G]
    {b : G} (hb : b â‰  0) {c : G} {f : Î± â†’ G} {l : Filter Î±} :
    Tendsto (f Â· / b) l (ğ“ (c / b)) â†” Tendsto f l (ğ“ c) := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ Filter.Tendsto.div_const' h bâŸ©
  convert h.div_const' bâ»Â¹ with k <;> rw [div_div, mul_inv_cancelâ‚€ hb, div_one]

lemma Filter.tendsto_sub_const_iff {G : Type*}
    [AddCommGroup G] [TopologicalSpace G] [ContinuousSub G]
    (b : G) {c : G} {f : Î± â†’ G} {l : Filter Î±} :
    Tendsto (f Â· - b) l (ğ“ (c - b)) â†” Tendsto f l (ğ“ c) := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ Filter.Tendsto.sub_const h bâŸ©
  convert h.sub_const (-b) with k <;> rw [sub_sub, â† sub_eq_add_neg, sub_self, sub_zero]

variable [TopologicalSpace Î±] {f g : Î± â†’ G} {s : Set Î±} {x : Î±}

@[to_additive (attr := continuity, fun_prop) sub]
theorem Continuous.div' (hf : Continuous f) (hg : Continuous g) : Continuous fun x => f x / g x :=
  continuous_div'.comp (hf.prod_mk hg : _)

@[to_additive (attr := continuity) continuous_sub_left]
lemma continuous_div_left' (a : G) : Continuous (a / Â·) := continuous_const.div' continuous_id

@[to_additive (attr := continuity) continuous_sub_right]
lemma continuous_div_right' (a : G) : Continuous (Â· / a) := continuous_id.div' continuous_const

@[to_additive (attr := fun_prop) sub]
theorem ContinuousAt.div' {f g : Î± â†’ G} {x : Î±} (hf : ContinuousAt f x) (hg : ContinuousAt g x) :
    ContinuousAt (fun x => f x / g x) x :=
  Filter.Tendsto.div' hf hg

@[to_additive sub]
theorem ContinuousWithinAt.div' (hf : ContinuousWithinAt f s x) (hg : ContinuousWithinAt g s x) :
    ContinuousWithinAt (fun x => f x / g x) s x :=
  Filter.Tendsto.div' hf hg

@[to_additive (attr := fun_prop) sub]
theorem ContinuousOn.div' (hf : ContinuousOn f s) (hg : ContinuousOn g s) :
    ContinuousOn (fun x => f x / g x) s := fun x hx => (hf x hx).div' (hg x hx)

end ContinuousDiv

section DivInvTopologicalGroup

variable [Group G] [TopologicalSpace G] [TopologicalGroup G]

/-- A version of `Homeomorph.mulLeft a bâ»Â¹` that is defeq to `a / b`. -/
@[to_additive (attr := simps! (config := { simpRhs := true }))
  " A version of `Homeomorph.addLeft a (-b)` that is defeq to `a - b`. "]
def Homeomorph.divLeft (x : G) : G â‰ƒâ‚œ G :=
  { Equiv.divLeft x with
    continuous_toFun := continuous_const.div' continuous_id
    continuous_invFun := continuous_inv.mul continuous_const }

@[to_additive]
theorem isOpenMap_div_left (a : G) : IsOpenMap (a / Â·) :=
  (Homeomorph.divLeft _).isOpenMap

@[to_additive]
theorem isClosedMap_div_left (a : G) : IsClosedMap (a / Â·) :=
  (Homeomorph.divLeft _).isClosedMap

/-- A version of `Homeomorph.mulRight aâ»Â¹ b` that is defeq to `b / a`. -/
@[to_additive (attr := simps! (config := { simpRhs := true }))
  "A version of `Homeomorph.addRight (-a) b` that is defeq to `b - a`. "]
def Homeomorph.divRight (x : G) : G â‰ƒâ‚œ G :=
  { Equiv.divRight x with
    continuous_toFun := continuous_id.div' continuous_const
    continuous_invFun := continuous_id.mul continuous_const }

@[to_additive]
lemma isOpenMap_div_right (a : G) : IsOpenMap (Â· / a) := (Homeomorph.divRight a).isOpenMap

@[to_additive]
lemma isClosedMap_div_right (a : G) : IsClosedMap (Â· / a) := (Homeomorph.divRight a).isClosedMap

@[to_additive]
theorem tendsto_div_nhds_one_iff {Î± : Type*} {l : Filter Î±} {x : G} {u : Î± â†’ G} :
    Tendsto (u Â· / x) l (ğ“ 1) â†” Tendsto u l (ğ“ x) :=
  haveI A : Tendsto (fun _ : Î± => x) l (ğ“ x) := tendsto_const_nhds
  âŸ¨fun h => by simpa using h.mul A, fun h => by simpa using h.div' AâŸ©

@[to_additive]
theorem nhds_translation_div (x : G) : comap (Â· / x) (ğ“ 1) = ğ“ x := by
  simpa only [div_eq_mul_inv] using nhds_translation_mul_inv x

end DivInvTopologicalGroup

/-!
### Topological operations on pointwise sums and products

A few results about interior and closure of the pointwise addition/multiplication of sets in groups
with continuous addition/multiplication. See also `Submonoid.top_closure_mul_self_eq` in
`Topology.Algebra.Monoid`.
-/


section ContinuousConstSMul

variable [TopologicalSpace Î²] [Group Î±] [MulAction Î± Î²] [ContinuousConstSMul Î± Î²] {s : Set Î±}
  {t : Set Î²}

variable [TopologicalSpace Î±]

@[to_additive]
theorem subset_interior_smul : interior s â€¢ interior t âŠ† interior (s â€¢ t) :=
  (Set.smul_subset_smul_right interior_subset).trans subset_interior_smul_right

end ContinuousConstSMul

section ContinuousSMul

variable [TopologicalSpace Î±] [TopologicalSpace Î²] [Group Î±] [MulAction Î± Î²] [ContinuousInv Î±]
  [ContinuousSMul Î± Î²] {s : Set Î±} {t : Set Î²}

@[to_additive]
theorem IsClosed.smul_left_of_isCompact (ht : IsClosed t) (hs : IsCompact s) :
    IsClosed (s â€¢ t) := by
  have : âˆ€ x âˆˆ s â€¢ t, âˆƒ g âˆˆ s, gâ»Â¹ â€¢ x âˆˆ t := by
    rintro x âŸ¨g, hgs, y, hyt, rflâŸ©
    refine âŸ¨g, hgs, ?_âŸ©
    rwa [inv_smul_smul]
  choose! f hf using this
  refine isClosed_of_closure_subset (fun x hx â†¦ ?_)
  rcases mem_closure_iff_ultrafilter.mp hx with âŸ¨u, hust, huxâŸ©
  have : Ultrafilter.map f u â‰¤ ğ“Ÿ s :=
    calc Ultrafilter.map f u â‰¤ map f (ğ“Ÿ (s â€¢ t)) := map_mono (le_principal_iff.mpr hust)
      _ = ğ“Ÿ (f '' (s â€¢ t)) := map_principal
      _ â‰¤ ğ“Ÿ s := principal_mono.mpr (image_subset_iff.mpr (fun x hx â†¦ (hf x hx).1))
  rcases hs.ultrafilter_le_nhds (Ultrafilter.map f u) this with âŸ¨g, hg, hugâŸ©
  suffices gâ»Â¹ â€¢ x âˆˆ t from
    âŸ¨g, hg, gâ»Â¹ â€¢ x, this, smul_inv_smul _ _âŸ©
  exact ht.mem_of_tendsto ((Tendsto.inv hug).smul hux)
    (Eventually.mono hust (fun y hy â†¦ (hf y hy).2))

/-! One may expect a version of `IsClosed.smul_left_of_isCompact` where `t` is compact and `s` is
closed, but such a lemma can't be true in this level of generality. For a counterexample, consider
`â„š` acting on `â„` by translation, and let `s : Set â„š := univ`, `t : set â„ := {0}`. Then `s` is
closed and `t` is compact, but `s +áµ¥ t` is the set of all rationals, which is definitely not
closed in `â„`.
To fix the proof, we would need to make two additional assumptions:
- for any `x âˆˆ t`, `s â€¢ {x}` is closed
- for any `x âˆˆ t`, there is a continuous function `g : s â€¢ {x} â†’ s` such that, for all
  `y âˆˆ s â€¢ {x}`, we have `y = (g y) â€¢ x`
These are fairly specific hypotheses so we don't state this version of the lemmas, but an
interesting fact is that these two assumptions are verified in the case of a `NormedAddTorsor`
(or really, any `AddTorsor` with continuous `-áµ¥`). We prove this special case in
`IsClosed.vadd_right_of_isCompact`. -/

@[to_additive]
theorem MulAction.isClosedMap_quotient [CompactSpace Î±] :
    letI := orbitRel Î± Î²
    IsClosedMap (Quotient.mk' : Î² â†’ Quotient (orbitRel Î± Î²)) := by
  intro t ht
  rw [â† quotientMap_quotient_mk'.isClosed_preimage, MulAction.quotient_preimage_image_eq_union_mul]
  convert ht.smul_left_of_isCompact (isCompact_univ (X := Î±))
  rw [â† biUnion_univ, â† iUnion_smul_left_image]
  rfl

end ContinuousSMul

section ContinuousConstSMul

variable [TopologicalSpace Î±] [Group Î±] [ContinuousConstSMul Î± Î±] {s t : Set Î±}

@[to_additive]
theorem IsOpen.mul_left : IsOpen t â†’ IsOpen (s * t) :=
  IsOpen.smul_left

@[to_additive]
theorem subset_interior_mul_right : s * interior t âŠ† interior (s * t) :=
  subset_interior_smul_right

@[to_additive]
theorem subset_interior_mul : interior s * interior t âŠ† interior (s * t) :=
  subset_interior_smul

@[to_additive]
theorem singleton_mul_mem_nhds (a : Î±) {b : Î±} (h : s âˆˆ ğ“ b) : {a} * s âˆˆ ğ“ (a * b) := by
  rwa [â† smul_eq_mul, â† smul_eq_mul, singleton_smul, smul_mem_nhds_smul_iff]

@[to_additive]
theorem singleton_mul_mem_nhds_of_nhds_one (a : Î±) (h : s âˆˆ ğ“ (1 : Î±)) : {a} * s âˆˆ ğ“ a := by
  simpa only [mul_one] using singleton_mul_mem_nhds a h

end ContinuousConstSMul

section ContinuousConstSMulOp

variable [TopologicalSpace Î±] [Group Î±] [ContinuousConstSMul Î±áµáµ’áµ– Î±] {s t : Set Î±}

@[to_additive]
theorem IsOpen.mul_right (hs : IsOpen s) : IsOpen (s * t) := by
  rw [â† image_op_smul]
  exact hs.smul_left

@[to_additive]
theorem subset_interior_mul_left : interior s * t âŠ† interior (s * t) :=
  interior_maximal (Set.mul_subset_mul_right interior_subset) isOpen_interior.mul_right

@[to_additive]
theorem subset_interior_mul' : interior s * interior t âŠ† interior (s * t) :=
  (Set.mul_subset_mul_left interior_subset).trans subset_interior_mul_left

@[to_additive]
theorem mul_singleton_mem_nhds (a : Î±) {b : Î±} (h : s âˆˆ ğ“ b) : s * {a} âˆˆ ğ“ (b * a) := by
  rw [mul_singleton]
  exact smul_mem_nhds_smul (op a) h

@[to_additive]
theorem mul_singleton_mem_nhds_of_nhds_one (a : Î±) (h : s âˆˆ ğ“ (1 : Î±)) : s * {a} âˆˆ ğ“ a := by
  simpa only [one_mul] using mul_singleton_mem_nhds a h

end ContinuousConstSMulOp

section TopologicalGroup

variable [TopologicalSpace G] [Group G] [TopologicalGroup G] {s t : Set G}

@[to_additive]
theorem IsOpen.div_left (ht : IsOpen t) : IsOpen (s / t) := by
  rw [â† iUnion_div_left_image]
  exact isOpen_biUnion fun a _ => isOpenMap_div_left a t ht

@[to_additive]
theorem IsOpen.div_right (hs : IsOpen s) : IsOpen (s / t) := by
  rw [â† iUnion_div_right_image]
  exact isOpen_biUnion fun a _ => isOpenMap_div_right a s hs

@[to_additive]
theorem subset_interior_div_left : interior s / t âŠ† interior (s / t) :=
  interior_maximal (div_subset_div_right interior_subset) isOpen_interior.div_right

@[to_additive]
theorem subset_interior_div_right : s / interior t âŠ† interior (s / t) :=
  interior_maximal (div_subset_div_left interior_subset) isOpen_interior.div_left

@[to_additive]
theorem subset_interior_div : interior s / interior t âŠ† interior (s / t) :=
  (div_subset_div_left interior_subset).trans subset_interior_div_left

@[to_additive]
theorem IsOpen.mul_closure (hs : IsOpen s) (t : Set G) : s * closure t = s * t := by
  refine (mul_subset_iff.2 fun a ha b hb => ?_).antisymm (mul_subset_mul_left subset_closure)
  rw [mem_closure_iff] at hb
  have hbU : b âˆˆ sâ»Â¹ * {a * b} := âŸ¨aâ»Â¹, Set.inv_mem_inv.2 ha, a * b, rfl, inv_mul_cancel_left _ _âŸ©
  obtain âŸ¨_, âŸ¨c, hc, d, rfl : d = _, rflâŸ©, hcsâŸ© := hb _ hs.inv.mul_right hbU
  exact âŸ¨câ»Â¹, hc, _, hcs, inv_mul_cancel_left _ _âŸ©

@[to_additive]
theorem IsOpen.closure_mul (ht : IsOpen t) (s : Set G) : closure s * t = s * t := by
  rw [â† inv_inv (closure s * t), mul_inv_rev, inv_closure, ht.inv.mul_closure, mul_inv_rev, inv_inv,
    inv_inv]

@[to_additive]
theorem IsOpen.div_closure (hs : IsOpen s) (t : Set G) : s / closure t = s / t := by
  simp_rw [div_eq_mul_inv, inv_closure, hs.mul_closure]

@[to_additive]
theorem IsOpen.closure_div (ht : IsOpen t) (s : Set G) : closure s / t = s / t := by
  simp_rw [div_eq_mul_inv, ht.inv.closure_mul]

@[to_additive]
theorem IsClosed.mul_left_of_isCompact (ht : IsClosed t) (hs : IsCompact s) : IsClosed (s * t) :=
  ht.smul_left_of_isCompact hs

@[to_additive]
theorem IsClosed.mul_right_of_isCompact (ht : IsClosed t) (hs : IsCompact s) :
    IsClosed (t * s) := by
  rw [â† image_op_smul]
  exact IsClosed.smul_left_of_isCompact ht (hs.image continuous_op)

@[to_additive]
theorem QuotientGroup.isClosedMap_coe {H : Subgroup G} (hH : IsCompact (H : Set G)) :
    IsClosedMap ((â†‘) : G â†’ G â§¸ H) := by
  intro t ht
  rw [â† (quotientMap_mk H).isClosed_preimage, preimage_image_mk_eq_mul]
  exact ht.mul_right_of_isCompact hH

@[to_additive]
lemma subset_mul_closure_one {G} [MulOneClass G] [TopologicalSpace G] (s : Set G) :
    s âŠ† s * (closure {1} : Set G) := by
  have : s âŠ† s * ({1} : Set G) := by simp
  exact this.trans (smul_subset_smul_left subset_closure)

@[to_additive]
lemma IsCompact.mul_closure_one_eq_closure {K : Set G} (hK : IsCompact K) :
    K * (closure {1} : Set G) = closure K := by
  apply Subset.antisymm ?_ ?_
  Â· calc
    K * (closure {1} : Set G) âŠ† closure K * (closure {1} : Set G) :=
      smul_subset_smul_right subset_closure
    _ âŠ† closure (K * ({1} : Set G)) := smul_set_closure_subset _ _
    _ = closure K := by simp
  Â· have : IsClosed (K * (closure {1} : Set G)) :=
      IsClosed.smul_left_of_isCompact isClosed_closure hK
    rw [IsClosed.closure_subset_iff this]
    exact subset_mul_closure_one K

@[to_additive]
lemma IsClosed.mul_closure_one_eq {F : Set G} (hF : IsClosed F) :
    F * (closure {1} : Set G) = F := by
  refine Subset.antisymm ?_ (subset_mul_closure_one F)
  calc
  F * (closure {1} : Set G) = closure F * closure ({1} : Set G) := by rw [hF.closure_eq]
  _ âŠ† closure (F * ({1} : Set G)) := smul_set_closure_subset _ _
  _ = F := by simp [hF.closure_eq]

@[to_additive]
lemma compl_mul_closure_one_eq {t : Set G} (ht : t * (closure {1} : Set G) = t) :
    tá¶œ * (closure {1} : Set G) = tá¶œ := by
  refine Subset.antisymm ?_ (subset_mul_closure_one tá¶œ)
  rintro - âŸ¨x, hx, g, hg, rflâŸ©
  by_contra H
  have : x âˆˆ t * (closure {1} : Set G) := by
    rw [â† Subgroup.coe_topologicalClosure_bot G] at hg âŠ¢
    simp only [smul_eq_mul, mem_compl_iff, not_not] at H
    exact âŸ¨x * g, H, gâ»Â¹, Subgroup.inv_mem _ hg, by simpâŸ©
  rw [ht] at this
  exact hx this

@[to_additive]
lemma compl_mul_closure_one_eq_iff {t : Set G} :
    tá¶œ * (closure {1} : Set G) = tá¶œ â†” t * (closure {1} : Set G) = t :=
  âŸ¨fun h â†¦ by simpa using compl_mul_closure_one_eq h, fun h â†¦ compl_mul_closure_one_eq hâŸ©

@[to_additive]
lemma IsOpen.mul_closure_one_eq {U : Set G} (hU : IsOpen U) :
    U * (closure {1} : Set G) = U :=
  compl_mul_closure_one_eq_iff.1 (hU.isClosed_compl.mul_closure_one_eq)

end TopologicalGroup

section FilterMul

section

variable (G) [TopologicalSpace G] [Group G] [ContinuousMul G]

@[to_additive]
theorem TopologicalGroup.t1Space (h : @IsClosed G _ {1}) : T1Space G :=
  âŸ¨fun x => by simpa using isClosedMap_mul_right x _ hâŸ©

end

section

variable (G) [TopologicalSpace G] [Group G] [TopologicalGroup G]

@[to_additive]
instance (priority := 100) TopologicalGroup.regularSpace : RegularSpace G := by
  refine .of_exists_mem_nhds_isClosed_subset fun a s hs â†¦ ?_
  have : Tendsto (fun p : G Ã— G => p.1 * p.2) (ğ“ (a, 1)) (ğ“ a) :=
    continuous_mul.tendsto' _ _ (mul_one a)
  rcases mem_nhds_prod_iff.mp (this hs) with âŸ¨U, hU, V, hV, hUVâŸ©
  rw [â† image_subset_iff, image_prod] at hUV
  refine âŸ¨closure U, mem_of_superset hU subset_closure, isClosed_closure, ?_âŸ©
  calc
    closure U âŠ† closure U * interior V := subset_mul_left _ (mem_interior_iff_mem_nhds.2 hV)
    _ = U * interior V := isOpen_interior.closure_mul U
    _ âŠ† U * V := mul_subset_mul_left interior_subset
    _ âŠ† s := hUV

-- `inferInstance` can find these instances now

variable {G}

@[to_additive]
theorem group_inseparable_iff {x y : G} : Inseparable x y â†” x / y âˆˆ closure (1 : Set G) := by
  rw [â† singleton_one, â† specializes_iff_mem_closure, specializes_comm, specializes_iff_inseparable,
    â† (Homeomorph.mulRight yâ»Â¹).embedding.inseparable_iff]
  simp [div_eq_mul_inv]

@[to_additive]
theorem TopologicalGroup.t2Space_iff_one_closed : T2Space G â†” IsClosed ({1} : Set G) :=
  âŸ¨fun _ â†¦ isClosed_singleton, fun h â†¦
    have := TopologicalGroup.t1Space G h; inferInstanceâŸ©

@[to_additive]
theorem TopologicalGroup.t2Space_of_one_sep (H : âˆ€ x : G, x â‰  1 â†’ âˆƒ U âˆˆ ğ“ (1 : G), x âˆ‰ U) :
    T2Space G := by
  suffices T1Space G from inferInstance
  refine t1Space_iff_specializes_imp_eq.2 fun x y hspec â†¦ by_contra fun hne â†¦ ?_
  rcases H (x * yâ»Â¹) (by rwa [Ne, mul_inv_eq_one]) with âŸ¨U, hUâ‚, hUâŸ©
  exact hU <| mem_of_mem_nhds <| hspec.map (continuous_mul_right yâ»Â¹) (by rwa [mul_inv_cancel])

/-- Given a neighborhood `U` of the identity, one may find a neighborhood `V` of the identity which
is closed, symmetric, and satisfies `V * V âŠ† U`. -/
@[to_additive "Given a neighborhood `U` of the identity, one may find a neighborhood `V` of the
identity which is closed, symmetric, and satisfies `V + V âŠ† U`."]
theorem exists_closed_nhds_one_inv_eq_mul_subset {U : Set G} (hU : U âˆˆ ğ“ 1) :
    âˆƒ V âˆˆ ğ“ 1, IsClosed V âˆ§ Vâ»Â¹ = V âˆ§ V * V âŠ† U := by
  rcases exists_open_nhds_one_mul_subset hU with âŸ¨V, V_open, V_mem, hVâŸ©
  rcases exists_mem_nhds_isClosed_subset (V_open.mem_nhds V_mem) with âŸ¨W, W_mem, W_closed, hWâŸ©
  refine âŸ¨W âˆ© Wâ»Â¹, Filter.inter_mem W_mem (inv_mem_nhds_one G W_mem), W_closed.inter W_closed.inv,
    by simp [inter_comm], ?_âŸ©
  calc
  W âˆ© Wâ»Â¹ * (W âˆ© Wâ»Â¹)
    âŠ† W * W := mul_subset_mul inter_subset_left inter_subset_left
  _ âŠ† V * V := mul_subset_mul hW hW
  _ âŠ† U := hV

variable (S : Subgroup G) [Subgroup.Normal S] [IsClosed (S : Set G)]

@[to_additive]
instance Subgroup.t3_quotient_of_isClosed (S : Subgroup G) [Subgroup.Normal S]
    [hS : IsClosed (S : Set G)] : T3Space (G â§¸ S) := by
  rw [â† QuotientGroup.ker_mk' S] at hS
  haveI := TopologicalGroup.t1Space (G â§¸ S) (quotientMap_quotient_mk'.isClosed_preimage.mp hS)
  infer_instance

/-- A subgroup `S` of a topological group `G` acts on `G` properly discontinuously on the left, if
it is discrete in the sense that `S âˆ© K` is finite for all compact `K`. (See also
`DiscreteTopology`.) -/
@[to_additive
  "A subgroup `S` of an additive topological group `G` acts on `G` properly
  discontinuously on the left, if it is discrete in the sense that `S âˆ© K` is finite for all compact
  `K`. (See also `DiscreteTopology`."]
theorem Subgroup.properlyDiscontinuousSMul_of_tendsto_cofinite (S : Subgroup G)
    (hS : Tendsto S.subtype cofinite (cocompact G)) : ProperlyDiscontinuousSMul S G :=
  { finite_disjoint_inter_image := by
      intro K L hK hL
      have H : Set.Finite _ := hS ((hL.prod hK).image continuous_div').compl_mem_cocompact
      rw [preimage_compl, compl_compl] at H
      convert H
      ext x
      simp only [image_smul, mem_setOf_eq, coeSubtype, mem_preimage, mem_image, Prod.exists]
      exact Set.smul_inter_ne_empty_iff' }

-- attribute [local semireducible] MulOpposite -- Porting note: doesn't work in Lean 4

/-- A subgroup `S` of a topological group `G` acts on `G` properly discontinuously on the right, if
it is discrete in the sense that `S âˆ© K` is finite for all compact `K`. (See also
`DiscreteTopology`.)

If `G` is Hausdorff, this can be combined with `t2Space_of_properlyDiscontinuousSMul_of_t2Space`
to show that the quotient group `G â§¸ S` is Hausdorff. -/
@[to_additive
  "A subgroup `S` of an additive topological group `G` acts on `G` properly discontinuously
  on the right, if it is discrete in the sense that `S âˆ© K` is finite for all compact `K`.
  (See also `DiscreteTopology`.)

  If `G` is Hausdorff, this can be combined with `t2Space_of_properlyDiscontinuousVAdd_of_t2Space`
  to show that the quotient group `G â§¸ S` is Hausdorff."]
theorem Subgroup.properlyDiscontinuousSMul_opposite_of_tendsto_cofinite (S : Subgroup G)
    (hS : Tendsto S.subtype cofinite (cocompact G)) : ProperlyDiscontinuousSMul S.op G :=
  { finite_disjoint_inter_image := by
      intro K L hK hL
      have : Continuous fun p : G Ã— G => (p.1â»Â¹, p.2) := continuous_inv.prod_map continuous_id
      have H : Set.Finite _ :=
        hS ((hK.prod hL).image (continuous_mul.comp this)).compl_mem_cocompact
      simp only [preimage_compl, compl_compl, coeSubtype, comp_apply] at H
      apply Finite.of_preimage _ (equivOp S).surjective
      convert H using 1
      ext x
      simp only [image_smul, mem_setOf_eq, coeSubtype, mem_preimage, mem_image, Prod.exists]
      exact Set.op_smul_inter_ne_empty_iff }

end

section

/-! Some results about an open set containing the product of two sets in a topological group. -/


variable [TopologicalSpace G] [MulOneClass G] [ContinuousMul G]

/-- Given a compact set `K` inside an open set `U`, there is an open neighborhood `V` of `1`
  such that `K * V âŠ† U`. -/
@[to_additive
  "Given a compact set `K` inside an open set `U`, there is an open neighborhood `V` of
  `0` such that `K + V âŠ† U`."]
theorem compact_open_separated_mul_right {K U : Set G} (hK : IsCompact K) (hU : IsOpen U)
    (hKU : K âŠ† U) : âˆƒ V âˆˆ ğ“ (1 : G), K * V âŠ† U := by
  refine hK.induction_on ?_ ?_ ?_ ?_
  Â· exact âŸ¨univ, by simpâŸ©
  Â· rintro s t hst âŸ¨V, hV, hV'âŸ©
    exact âŸ¨V, hV, (mul_subset_mul_right hst).trans hV'âŸ©
  Â· rintro s t âŸ¨V, V_in, hV'âŸ© âŸ¨W, W_in, hW'âŸ©
    use V âˆ© W, inter_mem V_in W_in
    rw [union_mul]
    exact
      union_subset ((mul_subset_mul_left V.inter_subset_left).trans hV')
        ((mul_subset_mul_left V.inter_subset_right).trans hW')
  Â· intro x hx
    have := tendsto_mul (show U âˆˆ ğ“ (x * 1) by simpa using hU.mem_nhds (hKU hx))
    rw [nhds_prod_eq, mem_map, mem_prod_iff] at this
    rcases this with âŸ¨t, ht, s, hs, hâŸ©
    rw [â† image_subset_iff, image_mul_prod] at h
    exact âŸ¨t, mem_nhdsWithin_of_mem_nhds ht, s, hs, hâŸ©

open MulOpposite

/-- Given a compact set `K` inside an open set `U`, there is an open neighborhood `V` of `1`
  such that `V * K âŠ† U`. -/
@[to_additive
  "Given a compact set `K` inside an open set `U`, there is an open neighborhood `V` of
  `0` such that `V + K âŠ† U`."]
theorem compact_open_separated_mul_left {K U : Set G} (hK : IsCompact K) (hU : IsOpen U)
    (hKU : K âŠ† U) : âˆƒ V âˆˆ ğ“ (1 : G), V * K âŠ† U := by
  rcases compact_open_separated_mul_right (hK.image continuous_op) (opHomeomorph.isOpenMap U hU)
      (image_subset op hKU) with
    âŸ¨V, hV : V âˆˆ ğ“ (op (1 : G)), hV' : op '' K * V âŠ† op '' UâŸ©
  refine âŸ¨op â»Â¹' V, continuous_op.continuousAt hV, ?_âŸ©
  rwa [â† image_preimage_eq V op_surjective, â† image_op_mul, image_subset_iff,
    preimage_image_eq _ op_injective] at hV'

end

section

variable [TopologicalSpace G] [Group G] [TopologicalGroup G]

/-- A compact set is covered by finitely many left multiplicative translates of a set
  with non-empty interior. -/
@[to_additive
  "A compact set is covered by finitely many left additive translates of a set
    with non-empty interior."]
theorem compact_covered_by_mul_left_translates {K V : Set G} (hK : IsCompact K)
    (hV : (interior V).Nonempty) : âˆƒ t : Finset G, K âŠ† â‹ƒ g âˆˆ t, (g * Â·) â»Â¹' V := by
  obtain âŸ¨t, htâŸ© : âˆƒ t : Finset G, K âŠ† â‹ƒ x âˆˆ t, interior ((x * Â·) â»Â¹' V) := by
    refine
      hK.elim_finite_subcover (fun x => interior <| (x * Â·) â»Â¹' V) (fun x => isOpen_interior) ?_
    cases' hV with gâ‚€ hgâ‚€
    refine fun g _ => mem_iUnion.2 âŸ¨gâ‚€ * gâ»Â¹, ?_âŸ©
    refine preimage_interior_subset_interior_preimage (continuous_const.mul continuous_id) ?_
    rwa [mem_preimage, Function.id_def, inv_mul_cancel_right]
  exact âŸ¨t, Subset.trans ht <| iUnionâ‚‚_mono fun g _ => interior_subsetâŸ©

/-- Every weakly locally compact separable topological group is Ïƒ-compact.
  Note: this is not true if we drop the topological group hypothesis. -/
@[to_additive SeparableWeaklyLocallyCompactAddGroup.sigmaCompactSpace
  "Every weakly locally compact separable topological additive group is Ïƒ-compact.
  Note: this is not true if we drop the topological group hypothesis."]
instance (priority := 100) SeparableWeaklyLocallyCompactGroup.sigmaCompactSpace [SeparableSpace G]
    [WeaklyLocallyCompactSpace G] : SigmaCompactSpace G := by
  obtain âŸ¨L, hLc, hL1âŸ© := exists_compact_mem_nhds (1 : G)
  refine âŸ¨âŸ¨fun n => (fun x => x * denseSeq G n) â»Â¹' L, ?_, ?_âŸ©âŸ©
  Â· intro n
    exact (Homeomorph.mulRight _).isCompact_preimage.mpr hLc
  Â· refine iUnion_eq_univ_iff.2 fun x => ?_
    obtain âŸ¨_, âŸ¨n, rflâŸ©, hnâŸ© : (range (denseSeq G) âˆ© (fun y => x * y) â»Â¹' L).Nonempty := by
      rw [â† (Homeomorph.mulLeft x).apply_symm_apply 1] at hL1
      exact (denseRange_denseSeq G).inter_nhds_nonempty
          ((Homeomorph.mulLeft x).continuous.continuousAt <| hL1)
    exact âŸ¨n, hnâŸ©

/-- Given two compact sets in a noncompact topological group, there is a translate of the second
one that is disjoint from the first one. -/
@[to_additive
  "Given two compact sets in a noncompact additive topological group, there is a
  translate of the second one that is disjoint from the first one."]
theorem exists_disjoint_smul_of_isCompact [NoncompactSpace G] {K L : Set G} (hK : IsCompact K)
    (hL : IsCompact L) : âˆƒ g : G, Disjoint K (g â€¢ L) := by
  have A : Â¬K * Lâ»Â¹ = univ := (hK.mul hL.inv).ne_univ
  obtain âŸ¨g, hgâŸ© : âˆƒ g, g âˆ‰ K * Lâ»Â¹ := by
    contrapose! A
    exact eq_univ_iff_forall.2 A
  refine âŸ¨g, ?_âŸ©
  refine disjoint_left.2 fun a ha h'a => hg ?_
  rcases h'a with âŸ¨b, bL, rflâŸ©
  refine âŸ¨g * b, ha, bâ»Â¹, by simpa only [Set.mem_inv, inv_inv] using bL, ?_âŸ©
  simp only [smul_eq_mul, mul_inv_cancel_right]

/-- A compact neighborhood of `1` in a topological group admits a closed compact subset
that is a neighborhood of `1`. -/
@[to_additive (attr := deprecated IsCompact.isCompact_isClosed_basis_nhds (since := "2024-01-28"))
  "A compact neighborhood of `0` in a topological additive group
admits a closed compact subset that is a neighborhood of `0`."]
theorem exists_isCompact_isClosed_subset_isCompact_nhds_one
    {L : Set G} (Lcomp : IsCompact L) (L1 : L âˆˆ ğ“ (1 : G)) :
    âˆƒ K : Set G, IsCompact K âˆ§ IsClosed K âˆ§ K âŠ† L âˆ§ K âˆˆ ğ“ (1 : G) :=
  let âŸ¨K, âŸ¨hK, hKâ‚, hKâ‚‚âŸ©, hKLâŸ© := (Lcomp.isCompact_isClosed_basis_nhds L1).mem_iff.1 L1
  âŸ¨K, hKâ‚, hKâ‚‚, hKL, hKâŸ©

/-- If a point in a topological group has a compact neighborhood, then the group is
locally compact. -/
@[to_additive]
theorem IsCompact.locallyCompactSpace_of_mem_nhds_of_group {K : Set G} (hK : IsCompact K) {x : G}
    (h : K âˆˆ ğ“ x) : LocallyCompactSpace G := by
  suffices WeaklyLocallyCompactSpace G from inferInstance
  refine âŸ¨fun y â†¦ âŸ¨(y * xâ»Â¹) â€¢ K, ?_, ?_âŸ©âŸ©
  Â· exact hK.smul _
  Â· rw [â† preimage_smul_inv]
    exact (continuous_const_smul _).continuousAt.preimage_mem_nhds (by simpa using h)

/-- A topological group which is weakly locally compact is automatically locally compact. -/
@[to_additive
  (attr := deprecated WeaklyLocallyCompactSpace.locallyCompactSpace (since := "2024-01-28"))]
theorem instLocallyCompactSpaceOfWeaklyOfGroup [WeaklyLocallyCompactSpace G] :
    LocallyCompactSpace G :=
  WeaklyLocallyCompactSpace.locallyCompactSpace

/-- If a function defined on a topological group has a support contained in a
compact set, then either the function is trivial or the group is locally compact. -/
@[to_additive
      "If a function defined on a topological additive group has a support contained in a compact
      set, then either the function is trivial or the group is locally compact."]
theorem eq_zero_or_locallyCompactSpace_of_support_subset_isCompact_of_group
    [TopologicalSpace Î±] [Zero Î±] [T1Space Î±]
    {f : G â†’ Î±} {k : Set G} (hk : IsCompact k) (hf : support f âŠ† k) (h'f : Continuous f) :
    f = 0 âˆ¨ LocallyCompactSpace G := by
  refine or_iff_not_imp_left.mpr fun h => ?_
  simp_rw [funext_iff, Pi.zero_apply] at h
  push_neg at h
  obtain âŸ¨x, hxâŸ© : âˆƒ x, f x â‰  0 := h
  have : k âˆˆ ğ“ x :=
    mem_of_superset (h'f.isOpen_support.mem_nhds hx) hf
  exact IsCompact.locallyCompactSpace_of_mem_nhds_of_group hk this

/-- If a function defined on a topological group has compact support, then either
the function is trivial or the group is locally compact. -/
@[to_additive
      "If a function defined on a topological additive group has compact support,
      then either the function is trivial or the group is locally compact."]
theorem HasCompactSupport.eq_zero_or_locallyCompactSpace_of_group
    [TopologicalSpace Î±] [Zero Î±] [T1Space Î±]
    {f : G â†’ Î±} (hf : HasCompactSupport f) (h'f : Continuous f) :
    f = 0 âˆ¨ LocallyCompactSpace G :=
  eq_zero_or_locallyCompactSpace_of_support_subset_isCompact_of_group hf (subset_tsupport f) h'f

/-- In a locally compact group, any neighborhood of the identity contains a compact closed
neighborhood of the identity, even without separation assumptions on the space. -/
@[to_additive (attr := deprecated isCompact_isClosed_basis_nhds (since := "2024-01-28"))
  "In a locally compact additive group, any neighborhood of the identity contains a
  compact closed neighborhood of the identity, even without separation assumptions on the space."]
theorem local_isCompact_isClosed_nhds_of_group [LocallyCompactSpace G] {U : Set G}
    (hU : U âˆˆ ğ“ (1 : G)) :
    âˆƒ K : Set G, IsCompact K âˆ§ IsClosed K âˆ§ K âŠ† U âˆ§ (1 : G) âˆˆ interior K :=
  let âŸ¨K, âŸ¨hKâ‚, hKco, hKclâŸ©, hKUâŸ© := (isCompact_isClosed_basis_nhds (1 : G)).mem_iff.1 hU
  âŸ¨K, hKco, hKcl, hKU, mem_interior_iff_mem_nhds.2 hKâ‚âŸ©

variable (G)

@[to_additive (attr := deprecated exists_mem_nhds_isCompact_isClosed (since := "2024-01-28"))]
theorem exists_isCompact_isClosed_nhds_one [WeaklyLocallyCompactSpace G] :
    âˆƒ K : Set G, IsCompact K âˆ§ IsClosed K âˆ§ K âˆˆ ğ“ 1 :=
  let âŸ¨K, hKâ‚, hKcomp, hKclâŸ© := exists_mem_nhds_isCompact_isClosed (1 : G)
  âŸ¨K, hKcomp, hKcl, hKâ‚âŸ©

/-- A quotient of a locally compact group is locally compact. -/
@[to_additive]
instance [LocallyCompactSpace G] (N : Subgroup G) : LocallyCompactSpace (G â§¸ N) := by
  refine âŸ¨fun x n hn â†¦ ?_âŸ©
  let Ï€ := ((â†‘) : G â†’ G â§¸ N)
  have C : Continuous Ï€ := continuous_quotient_mk'
  obtain âŸ¨y, rflâŸ© : âˆƒ y, Ï€ y = x := Quot.exists_rep x
  have : Ï€ â»Â¹' n âˆˆ ğ“ y := preimage_nhds_coinduced hn
  rcases local_compact_nhds this with âŸ¨s, s_mem, hs, s_compâŸ©
  exact âŸ¨Ï€ '' s, (QuotientGroup.isOpenMap_coe N).image_mem_nhds s_mem, mapsTo'.mp hs,
    s_comp.image CâŸ©

end

section

variable [TopologicalSpace G] [Group G] [TopologicalGroup G]

@[to_additive]
theorem nhds_mul (x y : G) : ğ“ (x * y) = ğ“ x * ğ“ y :=
  calc
    ğ“ (x * y) = map (x * Â·) (map (Â· * y) (ğ“ 1 * ğ“ 1)) := by simp
    _ = mapâ‚‚ (fun a b => x * (a * b * y)) (ğ“ 1) (ğ“ 1) := by rw [â† mapâ‚‚_mul, map_mapâ‚‚, map_mapâ‚‚]
    _ = mapâ‚‚ (fun a b => x * a * (b * y)) (ğ“ 1) (ğ“ 1) := by simp only [mul_assoc]
    _ = ğ“ x * ğ“ y := by
      rw [â† map_mul_left_nhds_one x, â† map_mul_right_nhds_one y, â† mapâ‚‚_mul, mapâ‚‚_map_left,
        mapâ‚‚_map_right]

/-- On a topological group, `ğ“ : G â†’ Filter G` can be promoted to a `MulHom`. -/
@[to_additive (attr := simps)
  "On an additive topological group, `ğ“ : G â†’ Filter G` can be promoted to an `AddHom`."]
def nhdsMulHom : G â†’â‚™* Filter G where
  toFun := ğ“
  map_mul' _ _ := nhds_mul _ _

end

end FilterMul

instance {G} [TopologicalSpace G] [Group G] [TopologicalGroup G] :
    TopologicalAddGroup (Additive G) where
  continuous_neg := @continuous_inv G _ _ _

instance {G} [TopologicalSpace G] [AddGroup G] [TopologicalAddGroup G] :
    TopologicalGroup (Multiplicative G) where
  continuous_inv := @continuous_neg G _ _ _

section Quotient

variable [Group G] [TopologicalSpace G] [ContinuousMul G] {Î“ : Subgroup G}

@[to_additive]
instance QuotientGroup.continuousConstSMul : ContinuousConstSMul G (G â§¸ Î“) where
  continuous_const_smul g := by
     convert ((@continuous_const _ _ _ _ g).mul continuous_id).quotient_map' _

@[to_additive]
theorem QuotientGroup.continuous_smulâ‚ (x : G â§¸ Î“) : Continuous fun g : G => g â€¢ x := by
  induction x using QuotientGroup.induction_on
  exact continuous_quotient_mk'.comp (continuous_mul_right _)

/-- The quotient of a second countable topological group by a subgroup is second countable. -/
@[to_additive
  "The quotient of a second countable additive topological group by a subgroup is second
  countable."]
instance QuotientGroup.secondCountableTopology [SecondCountableTopology G] :
    SecondCountableTopology (G â§¸ Î“) :=
  ContinuousConstSMul.secondCountableTopology

end Quotient

/-- If `G` is a group with topological `â»Â¹`, then it is homeomorphic to its units. -/
@[to_additive " If `G` is an additive group with topological negation, then it is homeomorphic to
its additive units."]
def toUnits_homeomorph [Group G] [TopologicalSpace G] [ContinuousInv G] : G â‰ƒâ‚œ GË£ where
  toEquiv := toUnits.toEquiv
  continuous_toFun := Units.continuous_iff.2 âŸ¨continuous_id, continuous_invâŸ©
  continuous_invFun := Units.continuous_val

@[to_additive] theorem Units.embedding_val [Group G] [TopologicalSpace G] [ContinuousInv G] :
    Embedding (val : GË£ â†’ G) :=
  toUnits_homeomorph.symm.embedding

namespace Units

open MulOpposite (continuous_op continuous_unop)

variable [Monoid Î±] [TopologicalSpace Î±] [Monoid Î²] [TopologicalSpace Î²]

@[to_additive]
instance [ContinuousMul Î±] : TopologicalGroup Î±Ë£ where
  continuous_inv := Units.continuous_iff.2 <| âŸ¨continuous_coe_inv, continuous_valâŸ©

/-- The topological group isomorphism between the units of a product of two monoids, and the product
of the units of each monoid. -/
@[to_additive
  "The topological group isomorphism between the additive units of a product of two
  additive monoids, and the product of the additive units of each additive monoid."]
def Homeomorph.prodUnits : (Î± Ã— Î²)Ë£ â‰ƒâ‚œ Î±Ë£ Ã— Î²Ë£ where
  continuous_toFun :=
    (continuous_fst.units_map (MonoidHom.fst Î± Î²)).prod_mk
      (continuous_snd.units_map (MonoidHom.snd Î± Î²))
  continuous_invFun :=
    Units.continuous_iff.2
      âŸ¨continuous_val.fst'.prod_mk continuous_val.snd',
        continuous_coe_inv.fst'.prod_mk continuous_coe_inv.snd'âŸ©
  toEquiv := MulEquiv.prodUnits.toEquiv

end Units

section LatticeOps

variable {Î¹ : Sort*} [Group G]

@[to_additive]
theorem topologicalGroup_sInf {ts : Set (TopologicalSpace G)}
    (h : âˆ€ t âˆˆ ts, @TopologicalGroup G t _) : @TopologicalGroup G (sInf ts) _ :=
  letI := sInf ts
  { toContinuousInv :=
      @continuousInv_sInf _ _ _ fun t ht => @TopologicalGroup.toContinuousInv G t _ <| h t ht
    toContinuousMul :=
      @continuousMul_sInf _ _ _ fun t ht =>
        @TopologicalGroup.toContinuousMul G t _ <| h t ht }

@[to_additive]
theorem topologicalGroup_iInf {ts' : Î¹ â†’ TopologicalSpace G}
    (h' : âˆ€ i, @TopologicalGroup G (ts' i) _) : @TopologicalGroup G (â¨… i, ts' i) _ := by
  rw [â† sInf_range]
  exact topologicalGroup_sInf (Set.forall_mem_range.mpr h')

@[to_additive]
theorem topologicalGroup_inf {tâ‚ tâ‚‚ : TopologicalSpace G} (hâ‚ : @TopologicalGroup G tâ‚ _)
    (hâ‚‚ : @TopologicalGroup G tâ‚‚ _) : @TopologicalGroup G (tâ‚ âŠ“ tâ‚‚) _ := by
  rw [inf_eq_iInf]
  refine topologicalGroup_iInf fun b => ?_
  cases b <;> assumption

end LatticeOps

/-!
### Lattice of group topologies

We define a type class `GroupTopology Î±` which endows a group `Î±` with a topology such that all
group operations are continuous.

Group topologies on a fixed group `Î±` are ordered, by reverse inclusion. They form a complete
lattice, with `âŠ¥` the discrete topology and `âŠ¤` the indiscrete topology.

Any function `f : Î± â†’ Î²` induces `coinduced f : TopologicalSpace Î± â†’ GroupTopology Î²`.

The additive version `AddGroupTopology Î±` and corresponding results are provided as well.
-/


/-- A group topology on a group `Î±` is a topology for which multiplication and inversion
are continuous. -/
structure GroupTopology (Î± : Type u) [Group Î±] extends TopologicalSpace Î±, TopologicalGroup Î± :
  Type u

/-- An additive group topology on an additive group `Î±` is a topology for which addition and
  negation are continuous. -/
structure AddGroupTopology (Î± : Type u) [AddGroup Î±] extends TopologicalSpace Î±,
  TopologicalAddGroup Î± : Type u

attribute [to_additive] GroupTopology

namespace GroupTopology

variable [Group Î±]

/-- A version of the global `continuous_mul` suitable for dot notation. -/
@[to_additive "A version of the global `continuous_add` suitable for dot notation."]
theorem continuous_mul' (g : GroupTopology Î±) :
    haveI := g.toTopologicalSpace
    Continuous fun p : Î± Ã— Î± => p.1 * p.2 := by
  letI := g.toTopologicalSpace
  haveI := g.toTopologicalGroup
  exact continuous_mul

/-- A version of the global `continuous_inv` suitable for dot notation. -/
@[to_additive "A version of the global `continuous_neg` suitable for dot notation."]
theorem continuous_inv' (g : GroupTopology Î±) :
    haveI := g.toTopologicalSpace
    Continuous (Inv.inv : Î± â†’ Î±) := by
  letI := g.toTopologicalSpace
  haveI := g.toTopologicalGroup
  exact continuous_inv

@[to_additive]
theorem toTopologicalSpace_injective :
    Function.Injective (toTopologicalSpace : GroupTopology Î± â†’ TopologicalSpace Î±) :=
  fun f g h => by
    cases f
    cases g
    congr

@[to_additive (attr := ext)]
theorem ext' {f g : GroupTopology Î±} (h : f.IsOpen = g.IsOpen) : f = g :=
  toTopologicalSpace_injective <| TopologicalSpace.ext h

/-- The ordering on group topologies on the group `Î³`. `t â‰¤ s` if every set open in `s` is also open
in `t` (`t` is finer than `s`). -/
@[to_additive
  "The ordering on group topologies on the group `Î³`. `t â‰¤ s` if every set open in `s`
  is also open in `t` (`t` is finer than `s`)."]
instance : PartialOrder (GroupTopology Î±) :=
  PartialOrder.lift toTopologicalSpace toTopologicalSpace_injective

@[to_additive (attr := simp)]
theorem toTopologicalSpace_le {x y : GroupTopology Î±} :
    x.toTopologicalSpace â‰¤ y.toTopologicalSpace â†” x â‰¤ y :=
  Iff.rfl

@[to_additive]
instance : Top (GroupTopology Î±) :=
  let _t : TopologicalSpace Î± := âŠ¤
  âŸ¨{  continuous_mul := continuous_top
      continuous_inv := continuous_top }âŸ©

@[to_additive (attr := simp)]
theorem toTopologicalSpace_top : (âŠ¤ : GroupTopology Î±).toTopologicalSpace = âŠ¤ :=
  rfl

@[to_additive]
instance : Bot (GroupTopology Î±) :=
  let _t : TopologicalSpace Î± := âŠ¥
  âŸ¨{  continuous_mul := by
        haveI := discreteTopology_bot Î±
        continuity
      continuous_inv := continuous_bot }âŸ©

@[to_additive (attr := simp)]
theorem toTopologicalSpace_bot : (âŠ¥ : GroupTopology Î±).toTopologicalSpace = âŠ¥ :=
  rfl

@[to_additive]
instance : BoundedOrder (GroupTopology Î±) where
  top := âŠ¤
  le_top x := show x.toTopologicalSpace â‰¤ âŠ¤ from le_top
  bot := âŠ¥
  bot_le x := show âŠ¥ â‰¤ x.toTopologicalSpace from bot_le

@[to_additive]
instance : Inf (GroupTopology Î±) where inf x y := âŸ¨x.1 âŠ“ y.1, topologicalGroup_inf x.2 y.2âŸ©

@[to_additive (attr := simp)]
theorem toTopologicalSpace_inf (x y : GroupTopology Î±) :
    (x âŠ“ y).toTopologicalSpace = x.toTopologicalSpace âŠ“ y.toTopologicalSpace :=
  rfl

@[to_additive]
instance : SemilatticeInf (GroupTopology Î±) :=
  toTopologicalSpace_injective.semilatticeInf _ toTopologicalSpace_inf

@[to_additive]
instance : Inhabited (GroupTopology Î±) :=
  âŸ¨âŠ¤âŸ©

/-- Infimum of a collection of group topologies. -/
@[to_additive "Infimum of a collection of additive group topologies"]
instance : InfSet (GroupTopology Î±) where
  sInf S :=
    âŸ¨sInf (toTopologicalSpace '' S), topologicalGroup_sInf <| forall_mem_image.2 fun t _ => t.2âŸ©

@[to_additive (attr := simp)]
theorem toTopologicalSpace_sInf (s : Set (GroupTopology Î±)) :
    (sInf s).toTopologicalSpace = sInf (toTopologicalSpace '' s) := rfl

@[to_additive (attr := simp)]
theorem toTopologicalSpace_iInf {Î¹} (s : Î¹ â†’ GroupTopology Î±) :
    (â¨… i, s i).toTopologicalSpace = â¨… i, (s i).toTopologicalSpace :=
  congr_arg sInf (range_comp _ _).symm

/-- Group topologies on `Î³` form a complete lattice, with `âŠ¥` the discrete topology and `âŠ¤` the
indiscrete topology.

The infimum of a collection of group topologies is the topology generated by all their open sets
(which is a group topology).

The supremum of two group topologies `s` and `t` is the infimum of the family of all group
topologies contained in the intersection of `s` and `t`. -/
@[to_additive
  "Group topologies on `Î³` form a complete lattice, with `âŠ¥` the discrete topology and
  `âŠ¤` the indiscrete topology.

  The infimum of a collection of group topologies is the topology generated by all their open sets
  (which is a group topology).

  The supremum of two group topologies `s` and `t` is the infimum of the family of all group
  topologies contained in the intersection of `s` and `t`."]
instance : CompleteSemilatticeInf (GroupTopology Î±) :=
  { inferInstanceAs (InfSet (GroupTopology Î±)),
    inferInstanceAs (PartialOrder (GroupTopology Î±)) with
    sInf_le := fun S a haS => toTopologicalSpace_le.1 <| sInf_le âŸ¨a, haS, rflâŸ©
    le_sInf := by
      intro S a hab
      apply (inferInstanceAs (CompleteLattice (TopologicalSpace Î±))).le_sInf
      rintro _ âŸ¨b, hbS, rflâŸ©
      exact hab b hbS }

@[to_additive]
instance : CompleteLattice (GroupTopology Î±) :=
  { inferInstanceAs (BoundedOrder (GroupTopology Î±)),
    inferInstanceAs (SemilatticeInf (GroupTopology Î±)),
    completeLatticeOfCompleteSemilatticeInf _ with
    inf := (Â· âŠ“ Â·)
    top := âŠ¤
    bot := âŠ¥ }

/-- Given `f : Î± â†’ Î²` and a topology on `Î±`, the coinduced group topology on `Î²` is the finest
topology such that `f` is continuous and `Î²` is a topological group. -/
@[to_additive
  "Given `f : Î± â†’ Î²` and a topology on `Î±`, the coinduced additive group topology on `Î²`
  is the finest topology such that `f` is continuous and `Î²` is a topological additive group."]
def coinduced {Î± Î² : Type*} [t : TopologicalSpace Î±] [Group Î²] (f : Î± â†’ Î²) : GroupTopology Î² :=
  sInf { b : GroupTopology Î² | TopologicalSpace.coinduced f t â‰¤ b.toTopologicalSpace }

@[to_additive]
theorem coinduced_continuous {Î± Î² : Type*} [t : TopologicalSpace Î±] [Group Î²] (f : Î± â†’ Î²) :
    Continuous[t, (coinduced f).toTopologicalSpace] f := by
  rw [continuous_sInf_rng]
  rintro _ âŸ¨t', ht', rflâŸ©
  exact continuous_iff_coinduced_le.2 ht'

end GroupTopology

set_option linter.style.longFile 2100
