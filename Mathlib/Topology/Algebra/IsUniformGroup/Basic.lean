/-
Copyright (c) 2018 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Johannes H√∂lzl
-/
import Mathlib.Topology.UniformSpace.UniformConvergence
import Mathlib.Topology.UniformSpace.UniformEmbedding
import Mathlib.Topology.UniformSpace.CompleteSeparated
import Mathlib.Topology.UniformSpace.Compact
import Mathlib.Topology.UniformSpace.HeineCantor
import Mathlib.Topology.Algebra.IsUniformGroup.Defs
import Mathlib.Topology.Algebra.Group.Quotient
import Mathlib.Topology.DiscreteSubset
import Mathlib.Tactic.Abel

/-!
# Uniform structure on topological groups

## Main results

* extension of ‚Ñ§-bilinear maps to complete groups (useful for ring completions)

* `QuotientGroup.completeSpace` and `QuotientAddGroup.completeSpace` guarantee that quotients
  of first countable topological groups by normal subgroups are themselves complete. In particular,
  the quotient of a Banach space by a subspace is complete.
-/

noncomputable section

open Uniformity Topology Filter Pointwise

section IsUniformGroup

open Filter Set

variable {Œ± : Type*} {Œ≤ : Type*}

variable [UniformSpace Œ±] [Group Œ±] [IsUniformGroup Œ±]

@[to_additive]
instance Pi.instIsUniformGroup {Œπ : Type*} {G : Œπ ‚Üí Type*} [‚àÄ i, UniformSpace (G i)]
    [‚àÄ i, Group (G i)] [‚àÄ i, IsUniformGroup (G i)] : IsUniformGroup (‚àÄ i, G i) where
  uniformContinuous_div := uniformContinuous_pi.mpr fun i ‚Ü¶
    (uniformContinuous_proj G i).comp uniformContinuous_fst |>.div <|
      (uniformContinuous_proj G i).comp uniformContinuous_snd

@[to_additive]
theorem isUniformEmbedding_translate_mul (a : Œ±) : IsUniformEmbedding fun x : Œ± => x * a :=
  { comap_uniformity := by
      nth_rw 1 [‚Üê uniformity_translate_mul a, comap_map]
      rintro ‚ü®p‚ÇÅ, p‚ÇÇ‚ü© ‚ü®q‚ÇÅ, q‚ÇÇ‚ü©
      simp only [Prod.mk.injEq, mul_left_inj, imp_self]
    injective := mul_left_injective a }

section Cauchy

namespace IsUniformGroup

variable {Œπ G : Type*} [Group G] [UniformSpace G] [IsUniformGroup G]

@[to_additive]
lemma cauchy_iff_tendsto (ùìï : Filter G) :
    Cauchy ùìï ‚Üî NeBot ùìï ‚àß Tendsto (fun p ‚Ü¶ p.1 / p.2) (ùìï √óÀ¢ ùìï) (ùìù 1) := by
  simp [Cauchy, uniformity_eq_comap_nhds_one_swapped, ‚Üê tendsto_iff_comap]

@[to_additive]
lemma cauchy_iff_tendsto_swapped (ùìï : Filter G) :
    Cauchy ùìï ‚Üî NeBot ùìï ‚àß Tendsto (fun p ‚Ü¶ p.2 / p.1) (ùìï √óÀ¢ ùìï) (ùìù 1) := by
  simp [Cauchy, uniformity_eq_comap_nhds_one, ‚Üê tendsto_iff_comap]

@[to_additive]
lemma cauchy_map_iff_tendsto (ùìï : Filter Œπ) (f : Œπ ‚Üí G) :
    Cauchy (map f ùìï) ‚Üî NeBot ùìï ‚àß Tendsto (fun p ‚Ü¶ f p.1 / f p.2) (ùìï √óÀ¢ ùìï) (ùìù 1) := by
  simp [cauchy_map_iff, uniformity_eq_comap_nhds_one_swapped, Function.comp_def]

@[to_additive]
lemma cauchy_map_iff_tendsto_swapped (ùìï : Filter Œπ) (f : Œπ ‚Üí G) :
    Cauchy (map f ùìï) ‚Üî NeBot ùìï ‚àß Tendsto (fun p ‚Ü¶ f p.2 / f p.1) (ùìï √óÀ¢ ùìï) (ùìù 1) := by
  simp [cauchy_map_iff, uniformity_eq_comap_nhds_one, Function.comp_def]

end IsUniformGroup

end Cauchy

section LatticeOps

variable [Group Œ≤]

@[to_additive]
lemma IsUniformInducing.isUniformGroup {Œ≥ : Type*} [Group Œ≥] [UniformSpace Œ≥] [IsUniformGroup Œ≥]
    [UniformSpace Œ≤] {F : Type*} [FunLike F Œ≤ Œ≥] [MonoidHomClass F Œ≤ Œ≥]
    (f : F) (hf : IsUniformInducing f) :
    IsUniformGroup Œ≤ where
  uniformContinuous_div := by
    simp_rw [hf.uniformContinuous_iff, Function.comp_def, map_div]
    exact uniformContinuous_div.comp (hf.uniformContinuous.prodMap hf.uniformContinuous)

@[deprecated (since := "2025-03-30")]
alias IsUniformInducing.uniformAddGroup := IsUniformInducing.isUniformAddGroup
@[to_additive existing, deprecated (since := "2025-03-30")]
alias IsUniformInducing.uniformGroup := IsUniformInducing.isUniformGroup

@[to_additive]
protected theorem IsUniformGroup.comap {Œ≥ : Type*} [Group Œ≥] {u : UniformSpace Œ≥} [IsUniformGroup Œ≥]
    {F : Type*} [FunLike F Œ≤ Œ≥] [MonoidHomClass F Œ≤ Œ≥] (f : F) : @IsUniformGroup Œ≤ (u.comap f) _ :=
  letI : UniformSpace Œ≤ := u.comap f; IsUniformInducing.isUniformGroup f ‚ü®rfl‚ü©

end LatticeOps

namespace Subgroup

@[to_additive]
instance isUniformGroup (S : Subgroup Œ±) : IsUniformGroup S := .comap S.subtype

end Subgroup

@[to_additive]
theorem CauchySeq.mul {Œπ : Type*} [Preorder Œπ] {u v : Œπ ‚Üí Œ±} (hu : CauchySeq u)
    (hv : CauchySeq v) : CauchySeq (u * v) :=
  uniformContinuous_mul.comp_cauchySeq (hu.prodMk hv)

@[to_additive]
theorem CauchySeq.mul_const {Œπ : Type*} [Preorder Œπ] {u : Œπ ‚Üí Œ±} {x : Œ±} (hu : CauchySeq u) :
    CauchySeq fun n => u n * x :=
  (uniformContinuous_id.mul uniformContinuous_const).comp_cauchySeq hu

@[to_additive]
theorem CauchySeq.const_mul {Œπ : Type*} [Preorder Œπ] {u : Œπ ‚Üí Œ±} {x : Œ±} (hu : CauchySeq u) :
    CauchySeq fun n => x * u n :=
  (uniformContinuous_const.mul uniformContinuous_id).comp_cauchySeq hu

@[to_additive]
theorem CauchySeq.inv {Œπ : Type*} [Preorder Œπ] {u : Œπ ‚Üí Œ±} (h : CauchySeq u) :
    CauchySeq u‚Åª¬π :=
  uniformContinuous_inv.comp_cauchySeq h

@[to_additive]
theorem totallyBounded_iff_subset_finite_iUnion_nhds_one {s : Set Œ±} :
    TotallyBounded s ‚Üî ‚àÄ U ‚àà ùìù (1 : Œ±), ‚àÉ t : Set Œ±, t.Finite ‚àß s ‚äÜ ‚ãÉ y ‚àà t, y ‚Ä¢ U :=
  (ùìù (1 : Œ±)).basis_sets.uniformity_of_nhds_one_inv_mul_swapped.totallyBounded_iff.trans <| by
    simp [‚Üê preimage_smul_inv, preimage]

@[to_additive]
theorem totallyBounded_inv {s : Set Œ±} (hs : TotallyBounded s) : TotallyBounded (s‚Åª¬π) := by
  convert TotallyBounded.image hs uniformContinuous_inv
  aesop

section UniformConvergence

variable {Œπ : Type*} {l : Filter Œπ} {l' : Filter Œ≤} {f f' : Œπ ‚Üí Œ≤ ‚Üí Œ±} {g g' : Œ≤ ‚Üí Œ±} {s : Set Œ≤}

@[to_additive]
theorem TendstoUniformlyOnFilter.mul (hf : TendstoUniformlyOnFilter f g l l')
    (hf' : TendstoUniformlyOnFilter f' g' l l') : TendstoUniformlyOnFilter (f * f') (g * g') l l' :=
  fun u hu =>
  ((uniformContinuous_mul.comp_tendstoUniformlyOnFilter (hf.prodMk hf')) u hu).diag_of_prod_left

@[to_additive]
theorem TendstoUniformlyOnFilter.div (hf : TendstoUniformlyOnFilter f g l l')
    (hf' : TendstoUniformlyOnFilter f' g' l l') : TendstoUniformlyOnFilter (f / f') (g / g') l l' :=
  fun u hu =>
  ((uniformContinuous_div.comp_tendstoUniformlyOnFilter (hf.prodMk hf')) u hu).diag_of_prod_left

@[to_additive]
theorem TendstoUniformlyOn.mul (hf : TendstoUniformlyOn f g l s)
    (hf' : TendstoUniformlyOn f' g' l s) : TendstoUniformlyOn (f * f') (g * g') l s := fun u hu =>
  ((uniformContinuous_mul.comp_tendstoUniformlyOn (hf.prodMk hf')) u hu).diag_of_prod

@[to_additive]
theorem TendstoUniformlyOn.div (hf : TendstoUniformlyOn f g l s)
    (hf' : TendstoUniformlyOn f' g' l s) : TendstoUniformlyOn (f / f') (g / g') l s := fun u hu =>
  ((uniformContinuous_div.comp_tendstoUniformlyOn (hf.prodMk hf')) u hu).diag_of_prod

@[to_additive]
theorem TendstoUniformly.mul (hf : TendstoUniformly f g l) (hf' : TendstoUniformly f' g' l) :
    TendstoUniformly (f * f') (g * g') l := fun u hu =>
  ((uniformContinuous_mul.comp_tendstoUniformly (hf.prodMk hf')) u hu).diag_of_prod

@[to_additive]
theorem TendstoUniformly.div (hf : TendstoUniformly f g l) (hf' : TendstoUniformly f' g' l) :
    TendstoUniformly (f / f') (g / g') l := fun u hu =>
  ((uniformContinuous_div.comp_tendstoUniformly (hf.prodMk hf')) u hu).diag_of_prod

@[to_additive]
theorem UniformCauchySeqOn.mul (hf : UniformCauchySeqOn f l s) (hf' : UniformCauchySeqOn f' l s) :
    UniformCauchySeqOn (f * f') l s := fun u hu => by
  simpa using (uniformContinuous_mul.comp_uniformCauchySeqOn (hf.prod' hf')) u hu

@[to_additive]
theorem UniformCauchySeqOn.div (hf : UniformCauchySeqOn f l s) (hf' : UniformCauchySeqOn f' l s) :
    UniformCauchySeqOn (f / f') l s := fun u hu => by
  simpa using (uniformContinuous_div.comp_uniformCauchySeqOn (hf.prod' hf')) u hu

end UniformConvergence

end IsUniformGroup

section IsTopologicalGroup

open Filter

variable (G : Type*) [Group G] [TopologicalSpace G] [IsTopologicalGroup G]

attribute [local instance] IsTopologicalGroup.toUniformSpace

@[to_additive]
theorem topologicalGroup_is_uniform_of_compactSpace [CompactSpace G] : IsUniformGroup G :=
  ‚ü®by
    apply CompactSpace.uniformContinuous_of_continuous
    exact continuous_div'‚ü©

variable {G}

@[to_additive]
instance Subgroup.isClosed_of_discrete [T2Space G] {H : Subgroup G} [DiscreteTopology H] :
    IsClosed (H : Set G) := by
  obtain ‚ü®V, V_in, VH‚ü© : ‚àÉ (V : Set G), V ‚àà ùìù (1 : G) ‚àß V ‚à© (H : Set G) = {1} :=
    nhds_inter_eq_singleton_of_mem_discrete H.one_mem
  have : (fun p : G √ó G => p.2 / p.1) ‚Åª¬π' V ‚àà ùì§ G := preimage_mem_comap V_in
  apply isClosed_of_spaced_out this
  intro h h_in h' h'_in
  contrapose!
  simp only [Set.mem_preimage, not_not]
  rintro (hyp : h' / h ‚àà V)
  have : h' / h ‚àà ({1} : Set G) := VH ‚ñ∏ Set.mem_inter hyp (H.div_mem h'_in h_in)
  exact (eq_of_div_eq_one this).symm

@[to_additive]
lemma Subgroup.tendsto_coe_cofinite_of_discrete [T2Space G] (H : Subgroup G) [DiscreteTopology H] :
    Tendsto ((‚Üë) : H ‚Üí G) cofinite (cocompact _) :=
  IsClosed.tendsto_coe_cofinite_of_discreteTopology inferInstance inferInstance

@[to_additive]
lemma MonoidHom.tendsto_coe_cofinite_of_discrete [T2Space G] {H : Type*} [Group H] {f : H ‚Üí* G}
    (hf : Function.Injective f) (hf' : DiscreteTopology f.range) :
    Tendsto f cofinite (cocompact _) := by
  replace hf : Function.Injective f.rangeRestrict := by simpa
  exact f.range.tendsto_coe_cofinite_of_discrete.comp hf.tendsto_cofinite

end IsTopologicalGroup

namespace IsTopologicalGroup

variable {Œπ Œ± G : Type*} [Group G] [u : UniformSpace G] [IsTopologicalGroup G]

@[to_additive]
theorem tendstoUniformly_iff (F : Œπ ‚Üí Œ± ‚Üí G) (f : Œ± ‚Üí G) (p : Filter Œπ)
    (hu : IsTopologicalGroup.toUniformSpace G = u) :
    TendstoUniformly F f p ‚Üî ‚àÄ u ‚àà ùìù (1 : G), ‚àÄ·∂† i in p, ‚àÄ a, F i a / f a ‚àà u :=
  hu ‚ñ∏ ‚ü®fun h u hu => h _ ‚ü®u, hu, fun _ => id‚ü©,
    fun h _ ‚ü®u, hu, hv‚ü© => mem_of_superset (h u hu) fun _ hi a => hv (hi a)‚ü©

@[to_additive]
theorem tendstoUniformlyOn_iff (F : Œπ ‚Üí Œ± ‚Üí G) (f : Œ± ‚Üí G) (p : Filter Œπ) (s : Set Œ±)
    (hu : IsTopologicalGroup.toUniformSpace G = u) :
    TendstoUniformlyOn F f p s ‚Üî ‚àÄ u ‚àà ùìù (1 : G), ‚àÄ·∂† i in p, ‚àÄ a ‚àà s, F i a / f a ‚àà u :=
  hu ‚ñ∏ ‚ü®fun h u hu => h _ ‚ü®u, hu, fun _ => id‚ü©,
    fun h _ ‚ü®u, hu, hv‚ü© => mem_of_superset (h u hu) fun _ hi a ha => hv (hi a ha)‚ü©

@[to_additive]
theorem tendstoLocallyUniformly_iff [TopologicalSpace Œ±] (F : Œπ ‚Üí Œ± ‚Üí G) (f : Œ± ‚Üí G)
    (p : Filter Œπ) (hu : IsTopologicalGroup.toUniformSpace G = u) :
    TendstoLocallyUniformly F f p ‚Üî
      ‚àÄ u ‚àà ùìù (1 : G), ‚àÄ (x : Œ±), ‚àÉ t ‚àà ùìù x, ‚àÄ·∂† i in p, ‚àÄ a ‚àà t, F i a / f a ‚àà u :=
  hu ‚ñ∏ ‚ü®fun h u hu => h _ ‚ü®u, hu, fun _ => id‚ü©, fun h _ ‚ü®u, hu, hv‚ü© x =>
    Exists.imp (fun _ ‚ü®h, hp‚ü© => ‚ü®h, mem_of_superset hp fun _ hi a ha => hv (hi a ha)‚ü©)
      (h u hu x)‚ü©

@[to_additive]
theorem tendstoLocallyUniformlyOn_iff [TopologicalSpace Œ±] (F : Œπ ‚Üí Œ± ‚Üí G) (f : Œ± ‚Üí G)
    (p : Filter Œπ) (s : Set Œ±) (hu : IsTopologicalGroup.toUniformSpace G = u) :
    TendstoLocallyUniformlyOn F f p s ‚Üî
      ‚àÄ u ‚àà ùìù (1 : G), ‚àÄ x ‚àà s, ‚àÉ t ‚àà ùìù[s] x, ‚àÄ·∂† i in p, ‚àÄ a ‚àà t, F i a / f a ‚àà u :=
  hu ‚ñ∏ ‚ü®fun h u hu => h _ ‚ü®u, hu, fun _ => id‚ü©, fun h _ ‚ü®u, hu, hv‚ü© x =>
    (Exists.imp fun _ ‚ü®h, hp‚ü© => ‚ü®h, mem_of_superset hp fun _ hi a ha => hv (hi a ha)‚ü©) ‚àò
      h u hu x‚ü©

end IsTopologicalGroup

open Filter Set Function

namespace IsDenseInducing

variable {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*} {Œ¥ : Type*}
variable {G : Type*}

-- Œ≤ is a dense subgroup of Œ±, inclusion is denoted by e
-- Œ¥ is a dense subgroup of Œ≥, inclusion is denoted by f
variable [TopologicalSpace Œ±] [AddCommGroup Œ±] [IsTopologicalAddGroup Œ±]
variable [TopologicalSpace Œ≤] [AddCommGroup Œ≤]
variable [TopologicalSpace Œ≥] [AddCommGroup Œ≥] [IsTopologicalAddGroup Œ≥]
variable [TopologicalSpace Œ¥] [AddCommGroup Œ¥]
variable [UniformSpace G] [AddCommGroup G]
variable {e : Œ≤ ‚Üí+ Œ±} (de : IsDenseInducing e)
variable {f : Œ¥ ‚Üí+ Œ≥} (df : IsDenseInducing f)
variable {œÜ : Œ≤ ‚Üí+ Œ¥ ‚Üí+ G}
variable (hœÜ : Continuous (fun p : Œ≤ √ó Œ¥ => œÜ p.1 p.2))
variable {W' : Set G} (W'_nhds : W' ‚àà ùìù (0 : G))
include de hœÜ

include W'_nhds in
private theorem extend_Z_bilin_aux (x‚ÇÄ : Œ±) (y‚ÇÅ : Œ¥) : ‚àÉ U‚ÇÇ ‚àà comap e (ùìù x‚ÇÄ), ‚àÄ x ‚àà U‚ÇÇ, ‚àÄ x' ‚àà U‚ÇÇ,
    (fun p : Œ≤ √ó Œ¥ => œÜ p.1 p.2) (x' - x, y‚ÇÅ) ‚àà W' := by
  let Nx := ùìù x‚ÇÄ
  let ee := fun u : Œ≤ √ó Œ≤ => (e u.1, e u.2)
  have lim1 : Tendsto (fun a : Œ≤ √ó Œ≤ => (a.2 - a.1, y‚ÇÅ))
      (comap e Nx √óÀ¢ comap e Nx) (ùìù (0, y‚ÇÅ)) := by
    have := Tendsto.prodMk (tendsto_sub_comap_self de x‚ÇÄ)
      (tendsto_const_nhds : Tendsto (fun _ : Œ≤ √ó Œ≤ => y‚ÇÅ) (comap ee <| ùìù (x‚ÇÄ, x‚ÇÄ)) (ùìù y‚ÇÅ))
    rw [nhds_prod_eq, prod_comap_comap_eq, ‚Üê nhds_prod_eq]
    exact (this :)
  have lim2 : Tendsto (fun p : Œ≤ √ó Œ¥ => œÜ p.1 p.2) (ùìù (0, y‚ÇÅ)) (ùìù 0) := by
    simpa using hœÜ.tendsto (0, y‚ÇÅ)
  have lim := lim2.comp lim1
  rw [tendsto_prod_self_iff] at lim
  simp_rw [forall_mem_comm]
  exact lim W' W'_nhds

variable [IsUniformAddGroup G]

include df W'_nhds in
private theorem extend_Z_bilin_key (x‚ÇÄ : Œ±) (y‚ÇÄ : Œ≥) : ‚àÉ U ‚àà comap e (ùìù x‚ÇÄ), ‚àÉ V ‚àà comap f (ùìù y‚ÇÄ),
    ‚àÄ x ‚àà U, ‚àÄ x' ‚àà U, ‚àÄ (y) (_ : y ‚àà V) (y') (_ : y' ‚àà V),
    (fun p : Œ≤ √ó Œ¥ => œÜ p.1 p.2) (x', y') - (fun p : Œ≤ √ó Œ¥ => œÜ p.1 p.2) (x, y) ‚àà W' := by
  let ee := fun u : Œ≤ √ó Œ≤ => (e u.1, e u.2)
  let ff := fun u : Œ¥ √ó Œ¥ => (f u.1, f u.2)
  have lim_œÜ : Filter.Tendsto (fun p : Œ≤ √ó Œ¥ => œÜ p.1 p.2) (ùìù (0, 0)) (ùìù 0) := by
    simpa using hœÜ.tendsto (0, 0)
  have lim_œÜ_sub_sub :
    Tendsto (fun p : (Œ≤ √ó Œ≤) √ó Œ¥ √ó Œ¥ => (fun p : Œ≤ √ó Œ¥ => œÜ p.1 p.2) (p.1.2 - p.1.1, p.2.2 - p.2.1))
      ((comap ee <| ùìù (x‚ÇÄ, x‚ÇÄ)) √óÀ¢ (comap ff <| ùìù (y‚ÇÄ, y‚ÇÄ))) (ùìù 0) := by
    have lim_sub_sub :
      Tendsto (fun p : (Œ≤ √ó Œ≤) √ó Œ¥ √ó Œ¥ => (p.1.2 - p.1.1, p.2.2 - p.2.1))
        (comap ee (ùìù (x‚ÇÄ, x‚ÇÄ)) √óÀ¢ comap ff (ùìù (y‚ÇÄ, y‚ÇÄ))) (ùìù 0 √óÀ¢ ùìù 0) := by
      have := Filter.prod_mono (tendsto_sub_comap_self de x‚ÇÄ) (tendsto_sub_comap_self df y‚ÇÄ)
      rwa [prod_map_map_eq] at this
    rw [‚Üê nhds_prod_eq] at lim_sub_sub
    exact Tendsto.comp lim_œÜ lim_sub_sub
  rcases exists_nhds_zero_quarter W'_nhds with ‚ü®W, W_nhds, W4‚ü©
  have :
    ‚àÉ U‚ÇÅ ‚àà comap e (ùìù x‚ÇÄ), ‚àÉ V‚ÇÅ ‚àà comap f (ùìù y‚ÇÄ), ‚àÄ (x) (_ : x ‚àà U‚ÇÅ) (x') (_ : x' ‚àà U‚ÇÅ),
      ‚àÄ (y) (_ : y ‚àà V‚ÇÅ) (y') (_ : y' ‚àà V‚ÇÅ), (fun p : Œ≤ √ó Œ¥ => œÜ p.1 p.2) (x' - x, y' - y) ‚àà W := by
    rcases tendsto_prod_iff.1 lim_œÜ_sub_sub W W_nhds with ‚ü®U, U_in, V, V_in, H‚ü©
    rw [nhds_prod_eq, ‚Üê prod_comap_comap_eq, mem_prod_same_iff] at U_in V_in
    rcases U_in with ‚ü®U‚ÇÅ, U‚ÇÅ_in, HU‚ÇÅ‚ü©
    rcases V_in with ‚ü®V‚ÇÅ, V‚ÇÅ_in, HV‚ÇÅ‚ü©
    exists U‚ÇÅ, U‚ÇÅ_in, V‚ÇÅ, V‚ÇÅ_in
    intro x x_in x' x'_in y y_in y' y'_in
    exact H _ _ (HU‚ÇÅ (mk_mem_prod x_in x'_in)) (HV‚ÇÅ (mk_mem_prod y_in y'_in))
  rcases this with ‚ü®U‚ÇÅ, U‚ÇÅ_nhds, V‚ÇÅ, V‚ÇÅ_nhds, H‚ü©
  obtain ‚ü®x‚ÇÅ, x‚ÇÅ_in‚ü© : U‚ÇÅ.Nonempty := (de.comap_nhds_neBot _).nonempty_of_mem U‚ÇÅ_nhds
  obtain ‚ü®y‚ÇÅ, y‚ÇÅ_in‚ü© : V‚ÇÅ.Nonempty := (df.comap_nhds_neBot _).nonempty_of_mem V‚ÇÅ_nhds
  have cont_flip : Continuous fun p : Œ¥ √ó Œ≤ => œÜ.flip p.1 p.2 := by
    change Continuous ((fun p : Œ≤ √ó Œ¥ => œÜ p.1 p.2) ‚àò Prod.swap)
    exact hœÜ.comp continuous_swap
  rcases extend_Z_bilin_aux de hœÜ W_nhds x‚ÇÄ y‚ÇÅ with ‚ü®U‚ÇÇ, U‚ÇÇ_nhds, HU‚ü©
  rcases extend_Z_bilin_aux df cont_flip W_nhds y‚ÇÄ x‚ÇÅ with ‚ü®V‚ÇÇ, V‚ÇÇ_nhds, HV‚ü©
  exists U‚ÇÅ ‚à© U‚ÇÇ, inter_mem U‚ÇÅ_nhds U‚ÇÇ_nhds, V‚ÇÅ ‚à© V‚ÇÇ, inter_mem V‚ÇÅ_nhds V‚ÇÇ_nhds
  rintro x ‚ü®xU‚ÇÅ, xU‚ÇÇ‚ü© x' ‚ü®x'U‚ÇÅ, x'U‚ÇÇ‚ü© y ‚ü®yV‚ÇÅ, yV‚ÇÇ‚ü© y' ‚ü®y'V‚ÇÅ, y'V‚ÇÇ‚ü©
  have key_formula : œÜ x' y' - œÜ x y
    = œÜ (x' - x) y‚ÇÅ + œÜ (x' - x) (y' - y‚ÇÅ) + œÜ x‚ÇÅ (y' - y) + œÜ (x - x‚ÇÅ) (y' - y) := by simp; abel
  rw [key_formula]
  have h‚ÇÅ := HU x xU‚ÇÇ x' x'U‚ÇÇ
  have h‚ÇÇ := H x xU‚ÇÅ x' x'U‚ÇÅ y‚ÇÅ y‚ÇÅ_in y' y'V‚ÇÅ
  have h‚ÇÉ := HV y yV‚ÇÇ y' y'V‚ÇÇ
  have h‚ÇÑ := H x‚ÇÅ x‚ÇÅ_in x xU‚ÇÅ y yV‚ÇÅ y' y'V‚ÇÅ
  exact W4 h‚ÇÅ h‚ÇÇ h‚ÇÉ h‚ÇÑ

open IsDenseInducing

variable [T0Space G] [CompleteSpace G]

/-- Bourbaki GT III.6.5 Theorem I:
‚Ñ§-bilinear continuous maps from dense images into a complete Hausdorff group extend by continuity.
Note: Bourbaki assumes that Œ± and Œ≤ are also complete Hausdorff, but this is not necessary. -/
theorem extend_Z_bilin : Continuous (extend (de.prodMap df) (fun p : Œ≤ √ó Œ¥ => œÜ p.1 p.2)) := by
  refine continuous_extend_of_cauchy _ ?_
  rintro ‚ü®x‚ÇÄ, y‚ÇÄ‚ü©
  constructor
  ¬∑ apply NeBot.map
    apply comap_neBot
    intro U h
    rcases mem_closure_iff_nhds.1 ((de.prodMap df).dense (x‚ÇÄ, y‚ÇÄ)) U h with ‚ü®x, x_in, ‚ü®z, z_x‚ü©‚ü©
    exists z
    aesop
  ¬∑ suffices map (fun p : (Œ≤ √ó Œ¥) √ó Œ≤ √ó Œ¥ => (fun p : Œ≤ √ó Œ¥ => œÜ p.1 p.2) p.2 -
      (fun p : Œ≤ √ó Œ¥ => œÜ p.1 p.2) p.1)
        (comap (fun p : (Œ≤ √ó Œ¥) √ó Œ≤ √ó Œ¥ => ((e p.1.1, f p.1.2), (e p.2.1, f p.2.2)))
        (ùìù (x‚ÇÄ, y‚ÇÄ) √óÀ¢ ùìù (x‚ÇÄ, y‚ÇÄ))) ‚â§ ùìù 0 by
      rwa [uniformity_eq_comap_nhds_zero G, prod_map_map_eq, ‚Üê map_le_iff_le_comap, Filter.map_map,
        prod_comap_comap_eq]
    intro W' W'_nhds
    have key := extend_Z_bilin_key de df hœÜ W'_nhds x‚ÇÄ y‚ÇÄ
    rcases key with ‚ü®U, U_nhds, V, V_nhds, h‚ü©
    rw [mem_comap] at U_nhds
    rcases U_nhds with ‚ü®U', U'_nhds, U'_sub‚ü©
    rw [mem_comap] at V_nhds
    rcases V_nhds with ‚ü®V', V'_nhds, V'_sub‚ü©
    rw [mem_map, mem_comap, nhds_prod_eq]
    exists (U' √óÀ¢ V') √óÀ¢ U' √óÀ¢ V'
    rw [mem_prod_same_iff]
    have := prod_mem_prod U'_nhds V'_nhds
    grind

end IsDenseInducing

section CompleteQuotient

universe u

open TopologicalSpace

open Classical in
/-- The quotient `G ‚ß∏ N` of a complete first countable topological group `G` by a normal subgroup
is itself complete. [N. Bourbaki, *General Topology*, IX.3.1 Proposition 4][bourbaki1966b]

Because a topological group is not equipped with a `UniformSpace` instance by default, we must
explicitly provide it in order to consider completeness. See `QuotientGroup.completeSpace` for a
version in which `G` is already equipped with a uniform structure. -/
@[to_additive /-- The quotient `G ‚ß∏ N` of a complete first countable topological additive group
`G` by a normal additive subgroup is itself complete. Consequently, quotients of Banach spaces by
subspaces are complete. [N. Bourbaki, *General Topology*, IX.3.1 Proposition 4][bourbaki1966b]

Because an additive topological group is not equipped with a `UniformSpace` instance by default,
we must explicitly provide it in order to consider completeness. See
`QuotientAddGroup.completeSpace` for a version in which `G` is already equipped with a uniform
structure. -/]
instance QuotientGroup.completeSpace' (G : Type u) [Group G] [TopologicalSpace G]
    [IsTopologicalGroup G] [FirstCountableTopology G] (N : Subgroup G) [N.Normal]
    [@CompleteSpace G (IsTopologicalGroup.toUniformSpace G)] :
    @CompleteSpace (G ‚ß∏ N) (IsTopologicalGroup.toUniformSpace (G ‚ß∏ N)) := by
  /- Since `G ‚ß∏ N` is a topological group it is a uniform space, and since `G` is first countable
    the uniformities of both `G` and `G ‚ß∏ N` are countably generated. Moreover, we may choose a
    sequential antitone neighborhood basis `u` for `ùìù (1 : G)` so that `(u (n + 1)) ^ 2 ‚äÜ u n`, and
    this descends to an antitone neighborhood basis `v` for `ùìù (1 : G ‚ß∏ N)`. Since `ùì§ (G ‚ß∏ N)` is
    countably generated, it suffices to show any Cauchy sequence `x` converges. -/
  letI : UniformSpace (G ‚ß∏ N) := IsTopologicalGroup.toUniformSpace (G ‚ß∏ N)
  letI : UniformSpace G := IsTopologicalGroup.toUniformSpace G
  haveI : (ùì§ (G ‚ß∏ N)).IsCountablyGenerated := comap.isCountablyGenerated _ _
  obtain ‚ü®u, hu, u_mul‚ü© := IsTopologicalGroup.exists_antitone_basis_nhds_one G
  obtain ‚ü®hv, v_anti‚ü© := hu.map ((‚Üë) : G ‚Üí G ‚ß∏ N)
  rw [‚Üê QuotientGroup.nhds_eq N 1, QuotientGroup.mk_one] at hv
  refine UniformSpace.complete_of_cauchySeq_tendsto fun x hx => ?_
  /- Given `n : ‚Ñï`, for sufficiently large `a b : ‚Ñï`, given any lift of `x b`, we can find a lift
    of `x a` such that the quotient of the lifts lies in `u n`. -/
  have key‚ÇÄ : ‚àÄ i j : ‚Ñï, ‚àÉ M : ‚Ñï, j < M ‚àß ‚àÄ a b : ‚Ñï, M ‚â§ a ‚Üí M ‚â§ b ‚Üí
      ‚àÄ g : G, x b = g ‚Üí ‚àÉ g' : G, g / g' ‚àà u i ‚àß x a = g' := by
    have hùì§GN : (ùì§ (G ‚ß∏ N)).HasBasis (fun _ ‚Ü¶ True) fun i ‚Ü¶ { x | x.snd / x.fst ‚àà (‚Üë) '' u i } := by
      simpa [uniformity_eq_comap_nhds_one'] using hv.comap _
    rw [hùì§GN.cauchySeq_iff] at hx
    simp only [mem_setOf_eq, forall_true_left, mem_image] at hx
    intro i j
    rcases hx i with ‚ü®M, hM‚ü©
    refine ‚ü®max j M + 1, (le_max_left _ _).trans_lt (lt_add_one _), fun a b ha hb g hg => ?_‚ü©
    obtain ‚ü®y, y_mem, hy‚ü© :=
      hM a (((le_max_right j _).trans (lt_add_one _).le).trans ha) b
        (((le_max_right j _).trans (lt_add_one _).le).trans hb)
    refine
      ‚ü®y‚Åª¬π * g, by
        simpa only [div_eq_mul_inv, mul_inv_rev, inv_inv, mul_inv_cancel_left] using y_mem, ?_‚ü©
    rw [QuotientGroup.mk_mul, QuotientGroup.mk_inv, hy, hg, inv_div, div_mul_cancel]
  /- Inductively construct a subsequence `œÜ : ‚Ñï ‚Üí ‚Ñï` using `key‚ÇÄ` so that if `a b : ‚Ñï` exceed
    `œÜ (n + 1)`, then we may find lifts whose quotients lie within `u n`. -/
  set œÜ : ‚Ñï ‚Üí ‚Ñï := fun n => Nat.recOn n (choose <| key‚ÇÄ 0 0) fun k yk => choose <| key‚ÇÄ (k + 1) yk
  have hœÜ :
    ‚àÄ n : ‚Ñï,
      œÜ n < œÜ (n + 1) ‚àß
        ‚àÄ a b : ‚Ñï,
          œÜ (n + 1) ‚â§ a ‚Üí
            œÜ (n + 1) ‚â§ b ‚Üí ‚àÄ g : G, x b = g ‚Üí ‚àÉ g' : G, g / g' ‚àà u (n + 1) ‚àß x a = g' :=
    fun n => choose_spec (key‚ÇÄ (n + 1) (œÜ n))
  /- Inductively construct a sequence `x' n : G` of lifts of `x (œÜ (n + 1))` such that quotients of
    successive terms lie in `x' n / x' (n + 1) ‚àà u (n + 1)`. We actually need the proofs that each
    term is a lift to construct the next term, so we use a Œ£-type. -/
  set x' : ‚àÄ n, PSigma fun g : G => x (œÜ (n + 1)) = g := fun n =>
    Nat.recOn n
      ‚ü®choose (QuotientGroup.mk_surjective (x (œÜ 1))),
        (choose_spec (QuotientGroup.mk_surjective (x (œÜ 1)))).symm‚ü©
      fun k hk =>
      ‚ü®choose <| (hœÜ k).2 _ _ (hœÜ (k + 1)).1.le le_rfl hk.fst hk.snd,
        (choose_spec <| (hœÜ k).2 _ _ (hœÜ (k + 1)).1.le le_rfl hk.fst hk.snd).2‚ü©
  have hx' : ‚àÄ n : ‚Ñï, (x' n).fst / (x' (n + 1)).fst ‚àà u (n + 1) := fun n =>
    (choose_spec <| (hœÜ n).2 _ _ (hœÜ (n + 1)).1.le le_rfl (x' n).fst (x' n).snd).1
  /- The sequence `x'` is Cauchy. This is where we exploit the condition on `u`. The key idea
    is to show by decreasing induction that `x' m / x' n ‚àà u m` if `m ‚â§ n`. -/
  have x'_cauchy : CauchySeq fun n => (x' n).fst := by
    have hùì§G : (ùì§ G).HasBasis (fun _ => True) fun i => { x | x.snd / x.fst ‚àà u i } := by
      simpa [uniformity_eq_comap_nhds_one'] using hu.toHasBasis.comap _
    rw [hùì§G.cauchySeq_iff']
    simp only [mem_setOf_eq, forall_true_left]
    exact fun m =>
      ‚ü®m, fun n hmn =>
        Nat.decreasingInduction'
          (fun k _ _ hk => u_mul k ‚ü®_, hx' k, _, hk, div_mul_div_cancel _ _ _‚ü©) hmn
          (by simpa only [div_self'] using mem_of_mem_nhds (hu.mem _))‚ü©
  /- Since `G` is complete, `x'` converges to some `x‚ÇÄ`, and so the image of this sequence under
    the quotient map converges to `‚Üëx‚ÇÄ`. The image of `x'` is a convergent subsequence of `x`, and
    since `x` is Cauchy, this implies it converges. -/
  rcases cauchySeq_tendsto_of_complete x'_cauchy with ‚ü®x‚ÇÄ, hx‚ÇÄ‚ü©
  refine
    ‚ü®‚Üëx‚ÇÄ,
      tendsto_nhds_of_cauchySeq_of_subseq hx
        (strictMono_nat_of_lt_succ fun n => (hœÜ (n + 1)).1).tendsto_atTop ?_‚ü©
  convert ((continuous_coinduced_rng : Continuous ((‚Üë) : G ‚Üí G ‚ß∏ N)).tendsto x‚ÇÄ).comp hx‚ÇÄ
  exact funext fun n => (x' n).snd

/-- The quotient `G ‚ß∏ N` of a complete first countable uniform group `G` by a normal subgroup
is itself complete. In contrast to `QuotientGroup.completeSpace'`, in this version `G` is
already equipped with a uniform structure.
[N. Bourbaki, *General Topology*, IX.3.1 Proposition 4][bourbaki1966b]

Even though `G` is equipped with a uniform structure, the quotient `G ‚ß∏ N` does not inherit a
uniform structure, so it is still provided manually via `IsTopologicalGroup.toUniformSpace`.
In the most common use cases, this coincides (definitionally) with the uniform structure on the
quotient obtained via other means. -/
@[to_additive /-- The quotient `G ‚ß∏ N` of a complete first countable uniform additive group
`G` by a normal additive subgroup is itself complete. Consequently, quotients of Banach spaces by
subspaces are complete. In contrast to `QuotientAddGroup.completeSpace'`, in this version
`G` is already equipped with a uniform structure.
[N. Bourbaki, *General Topology*, IX.3.1 Proposition 4][bourbaki1966b]

Even though `G` is equipped with a uniform structure, the quotient `G ‚ß∏ N` does not inherit a
uniform structure, so it is still provided manually via `IsTopologicalAddGroup.toUniformSpace`.
In the most common use case ‚îÄ quotients of normed additive commutative groups by subgroups ‚îÄ
significant care was taken so that the uniform structure inherent in that setting coincides
(definitionally) with the uniform structure provided here. -/]
instance QuotientGroup.completeSpace (G : Type u) [Group G] [us : UniformSpace G] [IsUniformGroup G]
    [FirstCountableTopology G] (N : Subgroup G) [N.Normal] [hG : CompleteSpace G] :
    @CompleteSpace (G ‚ß∏ N) (IsTopologicalGroup.toUniformSpace (G ‚ß∏ N)) := by
  rw [‚Üê @IsUniformGroup.toUniformSpace_eq _ us _ _] at hG
  infer_instance

end CompleteQuotient
