/-
Copyright (c) 2019 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Nailin Guan, Yi Song, Xuchun Li
-/
import Mathlib.RingTheory.Ideal.Basic
import Mathlib.Topology.Algebra.Group.Quotient
import Mathlib.Topology.Algebra.Ring.Basic
import Mathlib.Topology.Sets.Opens

/-!
# Open subgroups of a topological group

This files builds the lattice `OpenSubgroup G` of open subgroups in a topological group `G`,
and its additive version `OpenAddSubgroup`.  This lattice has a top element, the subgroup of all
elements, but no bottom element in general. The trivial subgroup which is the natural candidate
bottom has no reason to be open (this happens only in discrete groups).

Note that this notion is especially relevant in a non-archimedean context, for instance for
`p`-adic groups.

## Main declarations

* `OpenSubgroup.isClosed`: An open subgroup is automatically closed.
* `Subgroup.isOpen_mono`: A subgroup containing an open subgroup is open.
                           There are also versions for additive groups, submodules and ideals.
* `OpenSubgroup.comap`: Open subgroups can be pulled back by a continuous group morphism.

## TODO
* Prove that the identity component of a locally path connected group is an open subgroup.
  Up to now this file is really geared towards non-archimedean algebra, not Lie groups.
-/


open TopologicalSpace Topology Function

/-- The type of open subgroups of a topological additive group. -/
structure OpenAddSubgroup (G : Type*) [AddGroup G] [TopologicalSpace G] extends AddSubgroup G where
  isOpen' : IsOpen carrier

/-- The type of open subgroups of a topological group. -/
@[to_additive]
structure OpenSubgroup (G : Type*) [Group G] [TopologicalSpace G] extends Subgroup G where
  isOpen' : IsOpen carrier

/-- Reinterpret an `OpenSubgroup` as a `Subgroup`. -/
add_decl_doc OpenSubgroup.toSubgroup

/-- Reinterpret an `OpenAddSubgroup` as an `AddSubgroup`. -/
add_decl_doc OpenAddSubgroup.toAddSubgroup

attribute [coe] OpenSubgroup.toSubgroup OpenAddSubgroup.toAddSubgroup

namespace OpenSubgroup

variable {G : Type*} [Group G] [TopologicalSpace G]
variable {U V : OpenSubgroup G} {g : G}

@[to_additive]
instance hasCoeSubgroup : CoeTC (OpenSubgroup G) (Subgroup G) :=
  âŸ¨toSubgroupâŸ©

@[to_additive]
theorem toSubgroup_injective : Injective ((â†‘) : OpenSubgroup G â†’ Subgroup G)
  | âŸ¨_, _âŸ©, âŸ¨_, _âŸ©, rfl => rfl

@[to_additive]
instance : SetLike (OpenSubgroup G) G where
  coe U := U.1
  coe_injective' _ _ h := toSubgroup_injective <| SetLike.ext' h

@[to_additive]
instance : SubgroupClass (OpenSubgroup G) G where
  mul_mem := Subsemigroup.mul_mem' _
  one_mem U := U.one_mem'
  inv_mem := Subgroup.inv_mem' _

/-- Coercion from `OpenSubgroup G` to `Opens G`. -/
@[to_additive (attr := coe) "Coercion from `OpenAddSubgroup G` to `Opens G`."]
def toOpens (U : OpenSubgroup G) : Opens G := âŸ¨U, U.isOpen'âŸ©

@[to_additive]
instance hasCoeOpens : CoeTC (OpenSubgroup G) (Opens G) := âŸ¨toOpensâŸ©

@[to_additive (attr := simp, norm_cast)]
theorem coe_toOpens : ((U : Opens G) : Set G) = U :=
  rfl

@[to_additive (attr := simp, norm_cast)]
theorem coe_toSubgroup : ((U : Subgroup G) : Set G) = U := rfl

@[to_additive (attr := simp, norm_cast)]
theorem mem_toOpens : g âˆˆ (U : Opens G) â†” g âˆˆ U := Iff.rfl

@[to_additive (attr := simp, norm_cast)]
theorem mem_toSubgroup : g âˆˆ (U : Subgroup G) â†” g âˆˆ U := Iff.rfl

@[to_additive (attr := ext)]
theorem ext (h : âˆ€ x, x âˆˆ U â†” x âˆˆ V) : U = V :=
  SetLike.ext h

variable (U)

@[to_additive]
protected theorem isOpen : IsOpen (U : Set G) :=
  U.isOpen'

@[to_additive]
theorem mem_nhds_one : (U : Set G) âˆˆ ð“ (1 : G) :=
  U.isOpen.mem_nhds U.one_mem

variable {U}

@[to_additive] instance : Top (OpenSubgroup G) := âŸ¨âŸ¨âŠ¤, isOpen_univâŸ©âŸ©

@[to_additive (attr := simp)]
theorem mem_top (x : G) : x âˆˆ (âŠ¤ : OpenSubgroup G) :=
  trivial

@[to_additive (attr := simp, norm_cast)]
theorem coe_top : ((âŠ¤ : OpenSubgroup G) : Set G) = Set.univ :=
  rfl

@[to_additive (attr := simp, norm_cast)]
theorem toSubgroup_top : ((âŠ¤ : OpenSubgroup G) : Subgroup G) = âŠ¤ :=
  rfl

@[to_additive (attr := simp, norm_cast)]
theorem toOpens_top : ((âŠ¤ : OpenSubgroup G) : Opens G) = âŠ¤ :=
  rfl

@[to_additive]
instance : Inhabited (OpenSubgroup G) :=
  âŸ¨âŠ¤âŸ©

@[to_additive]
theorem isClosed [ContinuousMul G] (U : OpenSubgroup G) : IsClosed (U : Set G) := by
  apply isOpen_compl_iff.1
  refine isOpen_iff_forall_mem_open.2 fun x hx â†¦ âŸ¨(fun y â†¦ y * xâ»Â¹) â»Â¹' U, ?_, ?_, ?_âŸ©
  Â· refine fun u hux hu â†¦ hx ?_
    simp only [Set.mem_preimage, SetLike.mem_coe] at hux hu âŠ¢
    convert U.mul_mem (U.inv_mem hux) hu
    simp
  Â· exact U.isOpen.preimage (continuous_mul_right _)
  Â· simp [one_mem]

@[to_additive]
theorem isClopen [ContinuousMul G] (U : OpenSubgroup G) : IsClopen (U : Set G) :=
  âŸ¨U.isClosed, U.isOpenâŸ©

section

variable {H : Type*} [Group H] [TopologicalSpace H]

/-- The product of two open subgroups as an open subgroup of the product group. -/
@[to_additive "The product of two open subgroups as an open subgroup of the product group."]
def prod (U : OpenSubgroup G) (V : OpenSubgroup H) : OpenSubgroup (G Ã— H) :=
  âŸ¨.prod U V, U.isOpen.prod V.isOpenâŸ©

@[to_additive (attr := simp, norm_cast)]
theorem coe_prod (U : OpenSubgroup G) (V : OpenSubgroup H) :
    (U.prod V : Set (G Ã— H)) = (U : Set G) Ã—Ë¢ (V : Set H) :=
  rfl

@[to_additive (attr := simp, norm_cast)]
theorem toSubgroup_prod (U : OpenSubgroup G) (V : OpenSubgroup H) :
    (U.prod V : Subgroup (G Ã— H)) = (U : Subgroup G).prod V :=
  rfl

end

@[to_additive]
instance instInfOpenSubgroup : Inf (OpenSubgroup G) :=
  âŸ¨fun U V â†¦ âŸ¨U âŠ“ V, U.isOpen.inter V.isOpenâŸ©âŸ©

@[to_additive (attr := simp, norm_cast)]
theorem coe_inf : (â†‘(U âŠ“ V) : Set G) = (U : Set G) âˆ© V :=
  rfl

@[to_additive (attr := simp, norm_cast)]
theorem toSubgroup_inf : (â†‘(U âŠ“ V) : Subgroup G) = â†‘U âŠ“ â†‘V :=
  rfl

@[to_additive (attr := simp, norm_cast)]
theorem toOpens_inf : (â†‘(U âŠ“ V) : Opens G) = â†‘U âŠ“ â†‘V :=
  rfl

@[to_additive (attr := simp)]
theorem mem_inf {x} : x âˆˆ U âŠ“ V â†” x âˆˆ U âˆ§ x âˆˆ V :=
  Iff.rfl

@[to_additive]
instance instPartialOrderOpenSubgroup : PartialOrder (OpenSubgroup G) := inferInstance

-- Porting note: we override `toPartialorder` to get better `le`
@[to_additive]
instance instSemilatticeInfOpenSubgroup : SemilatticeInf (OpenSubgroup G) :=
  { SetLike.coe_injective.semilatticeInf ((â†‘) : OpenSubgroup G â†’ Set G) fun _ _ â†¦ rfl with
    toInf := instInfOpenSubgroup
    toPartialOrder := instPartialOrderOpenSubgroup }

@[to_additive]
instance : OrderTop (OpenSubgroup G) where
  top := âŠ¤
  le_top _ := Set.subset_univ _

@[to_additive (attr := simp, norm_cast)]
theorem toSubgroup_le : (U : Subgroup G) â‰¤ (V : Subgroup G) â†” U â‰¤ V :=
  Iff.rfl

variable {N : Type*} [Group N] [TopologicalSpace N]

/-- The preimage of an `OpenSubgroup` along a continuous `Monoid` homomorphism
  is an `OpenSubgroup`. -/
@[to_additive "The preimage of an `OpenAddSubgroup` along a continuous `AddMonoid` homomorphism
is an `OpenAddSubgroup`."]
def comap (f : G â†’* N) (hf : Continuous f) (H : OpenSubgroup N) : OpenSubgroup G :=
  âŸ¨.comap f H, H.isOpen.preimage hfâŸ©

@[to_additive (attr := simp, norm_cast)]
theorem coe_comap (H : OpenSubgroup N) (f : G â†’* N) (hf : Continuous f) :
    (H.comap f hf : Set G) = f â»Â¹' H :=
  rfl

@[to_additive (attr := simp, norm_cast)]
theorem toSubgroup_comap (H : OpenSubgroup N) (f : G â†’* N) (hf : Continuous f) :
    (H.comap f hf : Subgroup G) = (H : Subgroup N).comap f :=
  rfl

@[to_additive (attr := simp)]
theorem mem_comap {H : OpenSubgroup N} {f : G â†’* N} {hf : Continuous f} {x : G} :
    x âˆˆ H.comap f hf â†” f x âˆˆ H :=
  Iff.rfl

@[to_additive]
theorem comap_comap {P : Type*} [Group P] [TopologicalSpace P] (K : OpenSubgroup P) (fâ‚‚ : N â†’* P)
    (hfâ‚‚ : Continuous fâ‚‚) (fâ‚ : G â†’* N) (hfâ‚ : Continuous fâ‚) :
    (K.comap fâ‚‚ hfâ‚‚).comap fâ‚ hfâ‚ = K.comap (fâ‚‚.comp fâ‚) (hfâ‚‚.comp hfâ‚) :=
  rfl

end OpenSubgroup
namespace Subgroup

variable {G : Type*} [Group G] [TopologicalSpace G]

@[to_additive]
theorem isOpen_of_mem_nhds [ContinuousMul G] (H : Subgroup G) {g : G} (hg : (H : Set G) âˆˆ ð“ g) :
    IsOpen (H : Set G) := by
  refine isOpen_iff_mem_nhds.2 fun x hx â†¦ ?_
  have hg' : g âˆˆ H := SetLike.mem_coe.1 (mem_of_mem_nhds hg)
  have : Filter.Tendsto (fun y â†¦ y * (xâ»Â¹ * g)) (ð“ x) (ð“ g) :=
    (continuous_id.mul continuous_const).tendsto' _ _ (mul_inv_cancel_left _ _)
  simpa only [SetLike.mem_coe, Filter.mem_map',
    H.mul_mem_cancel_right (H.mul_mem (H.inv_mem hx) hg')] using this hg

@[to_additive]
theorem isOpen_mono [ContinuousMul G] {Hâ‚ Hâ‚‚ : Subgroup G} (h : Hâ‚ â‰¤ Hâ‚‚)
    (hâ‚ : IsOpen (Hâ‚ : Set G)) : IsOpen (Hâ‚‚ : Set G) :=
  isOpen_of_mem_nhds _ <| Filter.mem_of_superset (hâ‚.mem_nhds <| one_mem Hâ‚) h

@[to_additive]
theorem isOpen_of_openSubgroup [ContinuousMul G] (H: Subgroup G) {U : OpenSubgroup G} (h : â†‘U â‰¤ H) :
    IsOpen (H : Set G) :=
  isOpen_mono h U.isOpen

/-- If a subgroup of a topological group has `1` in its interior, then it is open. -/
@[to_additive "If a subgroup of an additive topological group has `0` in its interior, then it is
open."]
theorem isOpen_of_one_mem_interior [ContinuousMul G] (H: Subgroup G)
    (h_1_int : (1 : G) âˆˆ interior (H : Set G)) : IsOpen (H : Set G) :=
  isOpen_of_mem_nhds H <| mem_interior_iff_mem_nhds.1 h_1_int

@[to_additive]
lemma isClosed_of_isOpen [ContinuousMul G] (U : Subgroup G) (h : IsOpen (U : Set G)) :
    IsClosed (U : Set G) :=
  OpenSubgroup.isClosed âŸ¨U, hâŸ©

@[to_additive]
lemma subgroupOf_isOpen (U K : Subgroup G) (h : IsOpen (K : Set G)) :
    IsOpen (K.subgroupOf U : Set U) :=
  Continuous.isOpen_preimage (continuous_iff_le_induced.mpr fun _ â†¦ id) _ h

@[to_additive]
lemma discreteTopology [ContinuousMul G] (U : Subgroup G) (h : IsOpen (U : Set G)) :
    DiscreteTopology (G â§¸ U) := by
  refine singletons_open_iff_discrete.mp (fun g â†¦ ?_)
  induction' g using Quotient.inductionOn with g
  show IsOpen (QuotientGroup.mk â»Â¹' {QuotientGroup.mk g})
  convert_to IsOpen ((g * Â·) '' U)
  Â· ext g'
    simp only [Set.mem_preimage, Set.mem_singleton_iff, QuotientGroup.eq, Set.image_mul_left]
    rw [â† U.inv_mem_iff]
    simp
  Â· exact Homeomorph.mulLeft g |>.isOpen_image |>.mpr h

@[to_additive]
instance [ContinuousMul G] (U : OpenSubgroup G) : DiscreteTopology (G â§¸ U.toSubgroup) :=
  discreteTopology U.toSubgroup U.isOpen

@[to_additive]
lemma quotient_finite_of_isOpen [ContinuousMul G] [CompactSpace G] (U : Subgroup G)
    (h : IsOpen (U : Set G)) : Finite (G â§¸ U) :=
  have : DiscreteTopology (G â§¸ U) := U.discreteTopology h
  finite_of_compact_of_discrete

@[to_additive]
instance [ContinuousMul G] [CompactSpace G] (U : OpenSubgroup G) : Finite (G â§¸ U.toSubgroup) :=
  quotient_finite_of_isOpen U.toSubgroup U.isOpen

@[to_additive]
lemma quotient_finite_of_isOpen' [TopologicalGroup G] [CompactSpace G] (U : Subgroup G)
    (K : Subgroup U) (hUopen : IsOpen (U : Set G)) (hKopen : IsOpen (K : Set U)) :
    Finite (U â§¸ K) :=
  have : CompactSpace U := isCompact_iff_compactSpace.mp <| IsClosed.isCompact <|
    U.isClosed_of_isOpen hUopen
  K.quotient_finite_of_isOpen hKopen

@[to_additive]
instance [TopologicalGroup G] [CompactSpace G] (U : OpenSubgroup G) (K : OpenSubgroup U) :
    Finite (U â§¸ K.toSubgroup) :=
  quotient_finite_of_isOpen' U.toSubgroup K.toSubgroup U.isOpen K.isOpen

end Subgroup

namespace OpenSubgroup

variable {G : Type*} [Group G] [TopologicalSpace G] [ContinuousMul G]

@[to_additive]
instance : Sup (OpenSubgroup G) :=
  âŸ¨fun U V â†¦ âŸ¨U âŠ” V, Subgroup.isOpen_mono (le_sup_left : U.1 â‰¤ U.1 âŠ” V.1) U.isOpenâŸ©âŸ©

@[to_additive (attr := simp, norm_cast)]
theorem toSubgroup_sup (U V : OpenSubgroup G) : (â†‘(U âŠ” V) : Subgroup G) = â†‘U âŠ” â†‘V := rfl

-- Porting note: we override `toPartialorder` to get better `le`
@[to_additive]
instance : Lattice (OpenSubgroup G) :=
  { instSemilatticeInfOpenSubgroup,
    toSubgroup_injective.semilatticeSup ((â†‘) : OpenSubgroup G â†’ Subgroup G) fun _ _ â†¦ rfl with
    toPartialOrder := instPartialOrderOpenSubgroup }

end OpenSubgroup

namespace Submodule

open OpenAddSubgroup

variable {R : Type*} {M : Type*} [CommRing R]
variable [AddCommGroup M] [TopologicalSpace M] [TopologicalAddGroup M] [Module R M]

theorem isOpen_mono {U P : Submodule R M} (h : U â‰¤ P) (hU : IsOpen (U : Set M)) :
    IsOpen (P : Set M) :=
  @AddSubgroup.isOpen_mono M _ _ _ U.toAddSubgroup P.toAddSubgroup h hU

end Submodule

namespace Ideal

variable {R : Type*} [CommRing R]
variable [TopologicalSpace R] [TopologicalRing R]

theorem isOpen_of_isOpen_subideal {U I : Ideal R} (h : U â‰¤ I) (hU : IsOpen (U : Set R)) :
    IsOpen (I : Set R) :=
  @Submodule.isOpen_mono R R _ _ _ _ Semiring.toModule _ _ h hU

end Ideal

/-!
# Open normal subgroups of a topological group

This section builds the lattice `OpenNormalSubgroup G` of open subgroups in a topological group `G`,
and its additive version `OpenNormalAddSubgroup`.

-/

section

universe u

/-- The type of open normal subgroups of a topological group. -/
@[ext]
structure OpenNormalSubgroup (G : Type u) [Group G] [TopologicalSpace G]
  extends OpenSubgroup G where
  isNormal' : toSubgroup.Normal := by infer_instance

/-- The type of open normal subgroups of a topological additive group. -/
@[ext]
structure OpenNormalAddSubgroup (G : Type u) [AddGroup G] [TopologicalSpace G]
  extends OpenAddSubgroup G where
  isNormal' : toAddSubgroup.Normal := by infer_instance

attribute [to_additive] OpenNormalSubgroup

namespace OpenNormalSubgroup

variable {G : Type u} [Group G] [TopologicalSpace G]

@[to_additive]
instance (H : OpenNormalSubgroup G) : H.toSubgroup.Normal := H.isNormal'

@[to_additive]
theorem toSubgroup_injective : Function.Injective
    (fun H â†¦ H.toOpenSubgroup.toSubgroup : OpenNormalSubgroup G â†’ Subgroup G) :=
  fun A B h â†¦ by
  ext
  dsimp at h
  rw [h]

@[to_additive]
instance : SetLike (OpenNormalSubgroup G) G where
  coe U := U.1
  coe_injective' _ _ h := toSubgroup_injective <| SetLike.ext' h

@[to_additive]
instance : SubgroupClass (OpenNormalSubgroup G) G where
  mul_mem := Subsemigroup.mul_mem' _
  one_mem U := U.one_mem'
  inv_mem := Subgroup.inv_mem' _

@[to_additive]
instance : Coe (OpenNormalSubgroup G) (Subgroup G) where
  coe H := H.toOpenSubgroup.toSubgroup

@[to_additive]
instance instPartialOrderOpenNormalSubgroup : PartialOrder (OpenNormalSubgroup G) := inferInstance

@[to_additive]
instance instInfOpenNormalSubgroup : Inf (OpenNormalSubgroup G) :=
  âŸ¨fun U V â†¦ âŸ¨U.toOpenSubgroup âŠ“ V.toOpenSubgroup,
    Subgroup.normal_inf_normal U.toSubgroup V.toSubgroupâŸ©âŸ©

@[to_additive]
instance instSemilatticeInfOpenNormalSubgroup : SemilatticeInf (OpenNormalSubgroup G) :=
  SetLike.coe_injective.semilatticeInf ((â†‘) : OpenNormalSubgroup G â†’ Set G) fun _ _ â†¦ rfl

@[to_additive]
instance [ContinuousMul G] : Sup (OpenNormalSubgroup G) :=
  âŸ¨fun U V â†¦ âŸ¨U.toOpenSubgroup âŠ” V.toOpenSubgroup,
    Subgroup.sup_normal U.toOpenSubgroup.1 V.toOpenSubgroup.1âŸ©âŸ©

@[to_additive]
instance instSemilatticeSupOpenNormalSubgroup [ContinuousMul G] :
    SemilatticeSup (OpenNormalSubgroup G) :=
  toSubgroup_injective.semilatticeSup
    (fun (H : OpenNormalSubgroup G) â†¦ â†‘H.toOpenSubgroup) (fun _ _ â†¦ rfl)

@[to_additive]
instance [ContinuousMul G] : Lattice (OpenNormalSubgroup G) :=
  { instSemilatticeInfOpenNormalSubgroup,
    instSemilatticeSupOpenNormalSubgroup with
    toPartialOrder := instPartialOrderOpenNormalSubgroup}

end OpenNormalSubgroup

end

/-!
## The Open Subgroup in a Clopen Neighborhood of One
This section define `OpenSubgroupSubClopenNhdsOfOne` (`OpenNormalSubgroupSubClopenNhdsOfOne`),
which can pick an open subgroup contained in a clopen neighborhood of `1`.
-/

namespace TopologicalGroup

open scoped Pointwise

variable {G : Type*} [Group G]

/-- The first side of rectangle neighborhood of `(w,1)` in `mulClosurePairs`. -/
@[to_additive "The first side of rectangle neighborhood of `(w,1)` in `addClosurePairs`."]
def rectNhdSide [TopologicalSpace G]  [TopologicalGroup G]
    {W : Set G} (WOpen : IsOpen W) (einW : 1 âˆˆ W) (w : W) : Set G :=
  Classical.choose <| isOpen_prod_iff.mp
    (mulClosurePairs_open WOpen) w 1 (mem_mulClosurePairs einW w)

/-- The second side of rectangle neighborhood of `(w,1)` in `mulClosurePairs`. -/
@[to_additive "The second side of rectangle neighborhood of `(w,1)` in `addClosurePairs`."]
def rectNhdSideOne [TopologicalSpace G]  [TopologicalGroup G]
    {W : Set G} (WOpen : IsOpen W) (einW : 1 âˆˆ W) (w : W) : Set G :=
  Classical.choose <| Classical.choose_spec <| isOpen_prod_iff.mp
    (mulClosurePairs_open WOpen) w 1 (mem_mulClosurePairs einW w)

@[to_additive]
lemma rectNhdSide_open [TopologicalSpace G]  [TopologicalGroup G]
    {W : Set G} (WOpen : IsOpen W) (einW : 1 âˆˆ W) (w : W) :
    IsOpen (rectNhdSide WOpen einW w) :=
  (Classical.choose_spec <| Classical.choose_spec <| isOpen_prod_iff.mp
    (mulClosurePairs_open WOpen) w 1 (mem_mulClosurePairs einW w)).1

@[to_additive]
lemma rectNhdSideOne_open [TopologicalSpace G]  [TopologicalGroup G]
    {W : Set G} (WOpen : IsOpen W) (einW : 1 âˆˆ W) (w : W) :
    IsOpen (rectNhdSideOne WOpen einW w) :=
  (Classical.choose_spec <| Classical.choose_spec <| isOpen_prod_iff.mp
    (mulClosurePairs_open WOpen) w 1 (mem_mulClosurePairs einW w)).2.1

@[to_additive]
lemma mem_rectNhdSide [TopologicalSpace G]  [TopologicalGroup G]
    {W : Set G} (WOpen : IsOpen W) (einW : 1 âˆˆ W) (w : W) :
    w.1 âˆˆ (rectNhdSide WOpen einW w) :=
    (Classical.choose_spec <| Classical.choose_spec <| isOpen_prod_iff.mp
      (mulClosurePairs_open WOpen) w 1 (mem_mulClosurePairs einW w)).2.2.1

@[to_additive]
lemma one_mem_rectNhdSideOne [TopologicalSpace G]  [TopologicalGroup G]
    {W : Set G} (WOpen : IsOpen W) (einW : 1 âˆˆ W) (w : W) :
    1 âˆˆ (rectNhdSideOne WOpen einW w) :=
    (Classical.choose_spec <| Classical.choose_spec <| isOpen_prod_iff.mp
      (mulClosurePairs_open WOpen) w 1 (mem_mulClosurePairs einW w)).2.2.2.1

@[to_additive]
lemma nhds_side_mul_sub [TopologicalSpace G]  [TopologicalGroup G]
    {W : Set G} (WOpen : IsOpen W) (einW : 1 âˆˆ W) (w : W) :
    (rectNhdSide WOpen einW w) Ã—Ë¢ (rectNhdSideOne WOpen einW w) âŠ† mulClosurePairs W :=
  (Classical.choose_spec <| Classical.choose_spec <| isOpen_prod_iff.mp
    (mulClosurePairs_open WOpen) w 1 (mem_mulClosurePairs einW w)).2.2.2.2

/-- The symm core of `rectNhdSideOne`. -/
@[to_additive "The symm core of `rectNhdSideOne`."]
def rectNhdSideOneCore [TopologicalSpace G]  [TopologicalGroup G]
    {W : Set G} (WOpen : IsOpen W) (einW : 1 âˆˆ W) (w : W) : Set G :=
  Set.selfInvCore (rectNhdSideOne WOpen einW w)

@[to_additive]
lemma rectNhdSideOneCore_open [TopologicalSpace G]  [TopologicalGroup G]
    {W : Set G} (WOpen : IsOpen W) (einW : 1 âˆˆ W) (w : W) :
    IsOpen (rectNhdSideOneCore WOpen einW w) := by
  simp only [rectNhdSideOneCore, Set.selfInvCore]
  apply IsOpen.inter (rectNhdSideOne_open WOpen einW w)
    (IsOpen.inv (rectNhdSideOne_open WOpen einW w))

@[to_additive]
lemma one_mem_rectNhdSideOneCore [TopologicalSpace G]  [TopologicalGroup G]
    {W : Set G} (WOpen : IsOpen W) (einW : 1 âˆˆ W) (w : W) :
    1 âˆˆ (rectNhdSideOneCore WOpen einW w) := by
  simp only [rectNhdSideOneCore, Set.selfInvCore, Set.mem_inter_iff, Set.mem_inv, inv_one, and_self]
  exact (one_mem_rectNhdSideOne WOpen einW w)

@[to_additive]
lemma nhds_side_sub [TopologicalSpace G]  [TopologicalGroup G]
    {W : Set G} (WOpen : IsOpen W) (einW : 1 âˆˆ W) (w : W) :
    (rectNhdSide WOpen einW w) âŠ† W âˆ§ (rectNhdSideOne WOpen einW w) âŠ† W:= by
  have mulClosurePairssubWp : mulClosurePairs W âŠ† (W Ã—Ë¢ W) := Set.inter_subset_right
  have := Set.Subset.trans (nhds_side_mul_sub WOpen einW w) mulClosurePairssubWp
  rw [Set.prod_subset_prod_iff] at this
  rcases this with h | e1 | e2
  Â· exact h
  Â· absurd e1
    exact ne_of_mem_of_not_mem' (mem_rectNhdSide WOpen einW w) fun a â†¦ a
  Â· absurd e2
    exact ne_of_mem_of_not_mem' (one_mem_rectNhdSideOne WOpen einW w) fun a â†¦ a

@[to_additive]
lemma rectNhdSide_sub [TopologicalSpace G]  [TopologicalGroup G]
    {W : Set G} (WOpen : IsOpen W) (einW : 1 âˆˆ W) (w : W) :
    (rectNhdSide WOpen einW w) âŠ† W := (nhds_side_sub WOpen einW w).1

@[to_additive]
lemma rectNhdSideOneCore_sub [TopologicalSpace G]  [TopologicalGroup G]
    {W : Set G} (WOpen : IsOpen W) (einW : 1 âˆˆ W) (w : W) :
    (rectNhdSideOneCore WOpen einW w) âŠ† W := by
  simp only [rectNhdSideOneCore, Set.selfInvCore]
  exact Set.Subset.trans Set.inter_subset_left (nhds_side_sub WOpen einW w).2

@[to_additive]
lemma rectNhdSide_cover [TopologicalSpace G]  [TopologicalGroup G]
    {W : Set G} (WOpen : IsOpen W) (einW : 1 âˆˆ W) :
    W âŠ† â‹ƒ w : W, (rectNhdSide WOpen einW w) := by
  intro x xinW
  simp only [Set.iUnion_coe_set, Set.mem_iUnion]
  exact âŸ¨x, xinW, (mem_rectNhdSide WOpen einW âŸ¨x, xinWâŸ©)âŸ©

/-- The index of the finite subcover of the first side of the rectangle neighborhoods
covering `(W,1)`. -/
@[to_additive "The index of the finite subcover of the first side of the rectangle neighborhoods
covering `(W,1)`."]
private noncomputable def openSymmSubClopenNhdsOfOneIndex [TopologicalSpace G]  [TopologicalGroup G]
    [CompactSpace G] {W : Set G} (WClopen : IsClopen W) (einW : 1 âˆˆ W) : Finset W :=
  Classical.choose (IsCompact.elim_finite_subcover (IsClosed.isCompact (IsClopen.isClosed WClopen))
    _ (fun (w : W) â†¦
    (rectNhdSide_open WClopen.isOpen einW w)) (rectNhdSide_cover WClopen.isOpen einW))

@[to_additive]
private lemma openSymmSubClopenNhdsOfOneIndex_spec [TopologicalSpace G]  [TopologicalGroup G]
    [CompactSpace G] {W : Set G} (WClopen : IsClopen W) (einW : 1 âˆˆ W) :
    W âŠ† â‹ƒ i âˆˆ (openSymmSubClopenNhdsOfOneIndex WClopen einW), rectNhdSide WClopen.isOpen einW i :=
  Classical.choose_spec (IsCompact.elim_finite_subcover (IsClosed.isCompact
  (IsClopen.isClosed WClopen)) _ (fun (w : W) â†¦ (rectNhdSide_open WClopen.isOpen einW w))
  (rectNhdSide_cover WClopen.isOpen einW))

/-- An open symmetric neighborhood of `1` such that it is contained in a given
  clopen neighborhood of `1` and the given neighborhood is closed under multiplying by
  an element in it. -/
@[to_additive "An open symmetric neighborhood of `1` such that it is contained in a given
  clopen neighborhood of `1` and the given neighborhood is closed under adding by
  an element in it."]
def openSymmSubClopenNhdsOfOne [TopologicalSpace G]  [TopologicalGroup G]
    [CompactSpace G] {W : Set G} (WClopen : IsClopen W) (einW : 1 âˆˆ W) : Set G :=
  â‹‚ w âˆˆ (openSymmSubClopenNhdsOfOneIndex WClopen einW) , rectNhdSideOneCore WClopen.isOpen einW w

namespace openSymmSubClopenNhdsOfOne

variable [TopologicalSpace G]  [TopologicalGroup G]

@[to_additive]
lemma isopen [CompactSpace G] {W : Set G} (WClopen : IsClopen W) (einW : 1 âˆˆ W) :
    IsOpen (openSymmSubClopenNhdsOfOne WClopen einW) :=
  isOpen_biInter_finset (fun w _ â†¦ rectNhdSideOneCore_open WClopen.isOpen einW w)

@[to_additive]
lemma symm [CompactSpace G] {W : Set G} (WClopen : IsClopen W) (einW : 1 âˆˆ W) :
    (openSymmSubClopenNhdsOfOne WClopen einW) = (openSymmSubClopenNhdsOfOne WClopen einW)â»Â¹ := by
  simp only [openSymmSubClopenNhdsOfOne, rectNhdSideOneCore]
  apply Set.iInter_selfInvCore_symm

@[to_additive]
lemma one_mem [CompactSpace G] {W : Set G} (WClopen : IsClopen W) (einW : 1 âˆˆ W) :
    1 âˆˆ (openSymmSubClopenNhdsOfOne WClopen einW) := by
  simp only [openSymmSubClopenNhdsOfOne, Set.mem_iInter]
  exact fun w _ â†¦ one_mem_rectNhdSideOneCore WClopen.isOpen einW w

@[to_additive]
lemma mul_sub [CompactSpace G] {W : Set G} (WClopen : IsClopen W) (einW : 1 âˆˆ W) :
    W * (openSymmSubClopenNhdsOfOne WClopen einW) âŠ† W := by
  rintro a âŸ¨x,xinW,y,yinInter,xmulyâŸ©
  have := openSymmSubClopenNhdsOfOneIndex_spec WClopen einW xinW
  simp only [Set.mem_iUnion] at this
  rcases this with âŸ¨w,winfin,xinUâŸ©
  simp only [openSymmSubClopenNhdsOfOne, Set.iUnion_coe_set, Set.iInter_coe_set,
    Set.mem_iInter ] at yinInter
  have yinV := Set.mem_of_mem_inter_left (yinInter w w.2 winfin)
  simpa only [Set.mem_preimage, xmuly] using Set.mem_of_mem_inter_left <|
    nhds_side_mul_sub WClopen.isOpen einW w <| Set.mk_mem_prod xinU yinV

@[to_additive]
lemma sub [CompactSpace G] {W : Set G} (WClopen : IsClopen W) (einW : 1 âˆˆ W) :
    (openSymmSubClopenNhdsOfOne WClopen einW) âŠ† W :=
  Set.Subset.trans (Set.subset_mul_right (openSymmSubClopenNhdsOfOne WClopen einW) einW)
    (mul_sub WClopen einW)

end openSymmSubClopenNhdsOfOne

end TopologicalGroup

section

open scoped Pointwise

open TopologicalGroup

@[to_additive]
lemma iUnion_pow {G : Type*} [Group G] {V : Set G} (einV : 1 âˆˆ V) :
    {x : G | âˆƒ n : â„•, x âˆˆ V ^ n} = â‹ƒ n , V ^ (n + 1) := by
  ext x
  rw [Set.mem_setOf_eq, Set.mem_iUnion]
  refine âŸ¨fun âŸ¨n, hnâŸ© â†¦ ?_, fun âŸ¨n, _âŸ© â†¦ by use n + 1âŸ©
  by_cases h : n = 0
  Â· use 0
    simp only [h, pow_zero, Set.mem_one] at hn
    simpa only [zero_add, pow_one, hn] using einV
  Â· use n - 1
    rwa [Nat.sub_add_cancel <| Nat.one_le_iff_ne_zero.mpr h]

namespace TopologicalGroup

/-- An arbitrary open subgroup that is contained in a given clopen neighborhood of `1`. -/
@[to_additive "An open subgroup that is contained in a given clopen neighborhood of `1`."]
def OpenSubgroupSubClopenNhdsOfOne {G : Type*} [Group G] [TopologicalSpace G]  [TopologicalGroup G]
    [CompactSpace G] {W : Set G} (WClopen : IsClopen W) (einW : 1 âˆˆ W) : OpenSubgroup G where
  carrier := {x : G | âˆƒ n : â„•, x âˆˆ (openSymmSubClopenNhdsOfOne WClopen einW) ^ n}
  mul_mem':= by
    rintro a b âŸ¨na, hnaâŸ© âŸ¨nb, hnbâŸ©
    simp only [Set.mem_setOf_eq] at *
    use na + nb
    rw [pow_add]
    exact Set.mul_mem_mul hna hnb
  one_mem':= âŸ¨1, by simp only [pow_one, openSymmSubClopenNhdsOfOne.one_mem WClopen einW]âŸ©
  inv_mem':= by
    simp only [Set.mem_setOf_eq, forall_exists_index] at *
    intro x m hm
    use m
    rw [TopologicalGroup.openSymmSubClopenNhdsOfOne.symm]
    simpa only [inv_pow, Set.mem_inv, inv_inv] using hm
  isOpen' := by
    simp only [iUnion_pow (openSymmSubClopenNhdsOfOne.one_mem WClopen einW)]
    refine isOpen_iUnion (fun n â†¦ ?_)
    rw [pow_succ]
    exact IsOpen.mul_left (openSymmSubClopenNhdsOfOne.isopen WClopen einW)

@[to_additive]
theorem openSubgroupSubClopenNhdsOfOne_spec {G : Type*} [Group G] [TopologicalSpace G]
    [TopologicalGroup G] [CompactSpace G] {W : Set G} (WClopen : IsClopen W) (einW : 1 âˆˆ W) :
    ((OpenSubgroupSubClopenNhdsOfOne WClopen einW) : Set G) âŠ† W := by
  let V := (openSymmSubClopenNhdsOfOne WClopen einW)
  show {x : G | âˆƒ n : â„•, x âˆˆ V ^ n} âŠ† W
  simp_rw [iUnion_pow (openSymmSubClopenNhdsOfOne.one_mem WClopen einW), Set.iUnion_subset_iff]
  intro n _
  have mulVpow: W * V ^ (n + 1) âŠ† W := by
    induction' n with n ih
    Â· simp only [zero_add, pow_one, openSymmSubClopenNhdsOfOne.mul_sub WClopen einW]
    Â· rw [pow_succ, â† mul_assoc]
      exact le_trans (Set.mul_subset_mul_right ih) <|
        openSymmSubClopenNhdsOfOne.mul_sub WClopen einW
  apply le_trans _ mulVpow
  intro x xin
  rw [Set.mem_mul]
  use 1, einW, x, xin
  rw [one_mul]

theorem openSubgroupSubClopenNhdsOfOne_nonempty {G : Type*} [Group G] [TopologicalSpace G]
    [TopologicalGroup G] [CompactSpace G] {U : Set G} {UClopen : IsClopen U} {einU : 1 âˆˆ U} :
    Nonempty {H : OpenSubgroup G // (H : Set G) âŠ† U} :=
  Nonempty.intro âŸ¨OpenSubgroupSubClopenNhdsOfOne UClopen einU,
    openSubgroupSubClopenNhdsOfOne_spec UClopen einUâŸ©

end TopologicalGroup

end
