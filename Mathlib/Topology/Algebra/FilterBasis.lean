/-
Copyright (c) 2021 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
import Mathlib.Topology.Algebra.Module.Basic

/-!
# Group and ring filter bases

A `GroupFilterBasis` is a `FilterBasis` on a group with some properties relating
the basis to the group structure. The main theorem is that a `GroupFilterBasis`
on a group gives a topology on the group which makes it into a topological group
with neighborhoods of the neutral element generated by the given basis.

## Main definitions and results

Given a group `G` and a ring `R`:

* `GroupFilterBasis G`: the type of filter bases that will become neighborhood of `1`
  for a topology on `G` compatible with the group structure
* `GroupFilterBasis.topology`: the associated topology
* `GroupFilterBasis.isTopologicalGroup`: the compatibility between the above topology
  and the group structure
* `RingFilterBasis R`: the type of filter bases that will become neighborhood of `0`
  for a topology on `R` compatible with the ring structure
* `RingFilterBasis.topology`: the associated topology
* `RingFilterBasis.isTopologicalRing`: the compatibility between the above topology
  and the ring structure

## References

* [N. Bourbaki, *General Topology*][bourbaki1966]
-/


open Filter Set TopologicalSpace Function

open Topology Filter Pointwise

universe u

/-- A `GroupFilterBasis` on a group is a `FilterBasis` satisfying some additional axioms.
  Example : if `G` is a topological group then the neighbourhoods of the identity are a
  `GroupFilterBasis`. Conversely given a `GroupFilterBasis` one can define a topology
  compatible with the group structure on `G`. -/
class GroupFilterBasis (G : Type u) [Group G] extends FilterBasis G where
  one' : âˆ€ {U}, U âˆˆ sets â†’ (1 : G) âˆˆ U
  mul' : âˆ€ {U}, U âˆˆ sets â†’ âˆƒ V âˆˆ sets, V * V âŠ† U
  inv' : âˆ€ {U}, U âˆˆ sets â†’ âˆƒ V âˆˆ sets, V âŠ† (fun x â†¦ xâ»Â¹) â»Â¹' U
  conj' : âˆ€ xâ‚€, âˆ€ {U}, U âˆˆ sets â†’ âˆƒ V âˆˆ sets, V âŠ† (fun x â†¦ xâ‚€ * x * xâ‚€â»Â¹) â»Â¹' U

/-- An `AddGroupFilterBasis` on an additive group is a `FilterBasis` satisfying some additional
  axioms. Example : if `G` is a topological group then the neighbourhoods of the identity are an
  `AddGroupFilterBasis`. Conversely given an `AddGroupFilterBasis` one can define a topology
  compatible with the group structure on `G`. -/
class AddGroupFilterBasis (A : Type u) [AddGroup A] extends FilterBasis A where
  zero' : âˆ€ {U}, U âˆˆ sets â†’ (0 : A) âˆˆ U
  add' : âˆ€ {U}, U âˆˆ sets â†’ âˆƒ V âˆˆ sets, V + V âŠ† U
  neg' : âˆ€ {U}, U âˆˆ sets â†’ âˆƒ V âˆˆ sets, V âŠ† (fun x â†¦ -x) â»Â¹' U
  conj' : âˆ€ xâ‚€, âˆ€ {U}, U âˆˆ sets â†’ âˆƒ V âˆˆ sets, V âŠ† (fun x â†¦ xâ‚€ + x + -xâ‚€) â»Â¹' U

attribute [to_additive existing] GroupFilterBasis GroupFilterBasis.conj'
  GroupFilterBasis.toFilterBasis

/-- `GroupFilterBasis` constructor in the commutative group case. -/
@[to_additive /-- `AddGroupFilterBasis` constructor in the additive commutative group case. -/]
def groupFilterBasisOfComm {G : Type*} [CommGroup G] (sets : Set (Set G))
    (nonempty : sets.Nonempty) (inter_sets : âˆ€ x y, x âˆˆ sets â†’ y âˆˆ sets â†’ âˆƒ z âˆˆ sets, z âŠ† x âˆ© y)
    (one : âˆ€ U âˆˆ sets, (1 : G) âˆˆ U) (mul : âˆ€ U âˆˆ sets, âˆƒ V âˆˆ sets, V * V âŠ† U)
    (inv : âˆ€ U âˆˆ sets, âˆƒ V âˆˆ sets, V âŠ† (fun x â†¦ xâ»Â¹) â»Â¹' U) : GroupFilterBasis G :=
  { sets := sets
    nonempty := nonempty
    inter_sets := inter_sets _ _
    one' := one _
    mul' := mul _
    inv' := inv _
    conj' := fun x U U_in â†¦ âŸ¨U, U_in, by simp only [mul_inv_cancel_comm, preimage_id']; rflâŸ© }

namespace GroupFilterBasis

variable {G : Type u} [Group G] {B : GroupFilterBasis G}

@[to_additive]
instance : Membership (Set G) (GroupFilterBasis G) :=
  âŸ¨fun f s â†¦ s âˆˆ f.setsâŸ©

@[to_additive]
theorem one {U : Set G} : U âˆˆ B â†’ (1 : G) âˆˆ U :=
  GroupFilterBasis.one'

@[to_additive]
theorem mul {U : Set G} : U âˆˆ B â†’ âˆƒ V âˆˆ B, V * V âŠ† U :=
  GroupFilterBasis.mul'

@[to_additive]
theorem inv {U : Set G} : U âˆˆ B â†’ âˆƒ V âˆˆ B, V âŠ† (fun x â†¦ xâ»Â¹) â»Â¹' U :=
  GroupFilterBasis.inv'

@[to_additive]
theorem conj : âˆ€ xâ‚€, âˆ€ {U}, U âˆˆ B â†’ âˆƒ V âˆˆ B, V âŠ† (fun x â†¦ xâ‚€ * x * xâ‚€â»Â¹) â»Â¹' U :=
  GroupFilterBasis.conj'

/-- The trivial group filter basis consists of `{1}` only. The associated topology
is discrete. -/
@[to_additive /-- The trivial additive group filter basis consists of `{0}` only. The associated
topology is discrete. -/]
instance : Inhabited (GroupFilterBasis G) where
  default := {
    sets := {{1}}
    nonempty := singleton_nonempty _
    inter_sets := by simp
    one' := by simp
    mul' := by simp
    inv' := by simp
    conj' := by simp }

@[to_additive]
theorem subset_mul_self (B : GroupFilterBasis G) {U : Set G} (h : U âˆˆ B) : U âŠ† U * U :=
  fun x x_in â†¦ âŸ¨1, one h, x, x_in, one_mul xâŸ©

/-- The neighborhood function of a `GroupFilterBasis`. -/
@[to_additive /-- The neighborhood function of an `AddGroupFilterBasis`. -/]
def N (B : GroupFilterBasis G) : G â†’ Filter G :=
  fun x â†¦ map (fun y â†¦ x * y) B.toFilterBasis.filter

@[to_additive (attr := simp)]
theorem N_one (B : GroupFilterBasis G) : B.N 1 = B.toFilterBasis.filter := by
  simp only [N, one_mul, map_id']

@[to_additive]
protected theorem hasBasis (B : GroupFilterBasis G) (x : G) :
    HasBasis (B.N x) (fun V : Set G â†¦ V âˆˆ B) fun V â†¦ (fun y â†¦ x * y) '' V :=
  HasBasis.map (fun y â†¦ x * y) toFilterBasis.hasBasis

/-- The topological space structure coming from a group filter basis. -/
@[to_additive /-- The topological space structure coming from an additive group filter basis. -/]
def topology (B : GroupFilterBasis G) : TopologicalSpace G :=
  TopologicalSpace.mkOfNhds B.N

@[to_additive]
theorem nhds_eq (B : GroupFilterBasis G) {xâ‚€ : G} : @nhds G B.topology xâ‚€ = B.N xâ‚€ := by
  apply TopologicalSpace.nhds_mkOfNhds_of_hasBasis (fun x â†¦ (FilterBasis.hasBasis _).map _)
  Â· intro a U U_in
    exact âŸ¨1, B.one U_in, mul_one aâŸ©
  Â· intro a U U_in
    rcases GroupFilterBasis.mul U_in with âŸ¨V, V_in, hVUâŸ©
    filter_upwards [image_mem_map (B.mem_filter_of_mem V_in)]
    rintro _ âŸ¨x, hx, rflâŸ©
    calc
      (a * x) â€¢ V âˆˆ (a * x) â€¢ B.filter := smul_set_mem_smul_filter <| B.mem_filter_of_mem V_in
      _ = a â€¢ x â€¢ V := smul_smul .. |>.symm
      _ âŠ† a â€¢ (V * V) := smul_set_mono <| smul_set_subset_smul hx
      _ âŠ† a â€¢ U := smul_set_mono hVU

@[to_additive]
theorem nhds_one_eq (B : GroupFilterBasis G) :
    @nhds G B.topology (1 : G) = B.toFilterBasis.filter := by
  rw [B.nhds_eq]
  simp only [N, one_mul]
  exact map_id

@[to_additive]
theorem nhds_hasBasis (B : GroupFilterBasis G) (xâ‚€ : G) :
    HasBasis (@nhds G B.topology xâ‚€) (fun V : Set G â†¦ V âˆˆ B) fun V â†¦ (fun y â†¦ xâ‚€ * y) '' V := by
  rw [B.nhds_eq]
  apply B.hasBasis

@[to_additive]
theorem nhds_one_hasBasis (B : GroupFilterBasis G) :
    HasBasis (@nhds G B.topology 1) (fun V : Set G â†¦ V âˆˆ B) id := by
  rw [B.nhds_one_eq]
  exact B.toFilterBasis.hasBasis

@[to_additive]
theorem mem_nhds_one (B : GroupFilterBasis G) {U : Set G} (hU : U âˆˆ B) :
    U âˆˆ @nhds G B.topology 1 := by
  rw [B.nhds_one_hasBasis.mem_iff]
  exact âŸ¨U, hU, rfl.subsetâŸ©

-- See note [lower instance priority]
/-- If a group is endowed with a topological structure coming from a group filter basis then, it's a
topological group. -/
@[to_additive /-- If an additive group is endowed with a topological structure coming from an
additive group filter basis, then it's an additive topological group. -/]
instance (priority := 100) isTopologicalGroup (B : GroupFilterBasis G) :
    @IsTopologicalGroup G B.topology _ := by
  letI := B.topology
  have basis := B.nhds_one_hasBasis
  have basis' := basis.prod basis
  refine IsTopologicalGroup.of_nhds_one ?_ ?_ ?_ ?_
  Â· rw [basis'.tendsto_iff basis]
    suffices âˆ€ U âˆˆ B, âˆƒ V W, (V âˆˆ B âˆ§ W âˆˆ B) âˆ§ âˆ€ a b, a âˆˆ V â†’ b âˆˆ W â†’ a * b âˆˆ U by simpa
    intro U U_in
    rcases mul U_in with âŸ¨V, V_in, hVâŸ©
    refine âŸ¨V, V, âŸ¨V_in, V_inâŸ©, ?_âŸ©
    intro a b a_in b_in
    exact hV <| mul_mem_mul a_in b_in
  Â· rw [basis.tendsto_iff basis]
    intro U U_in
    simpa using inv U_in
  Â· intro xâ‚€
    rw [nhds_eq, nhds_one_eq]
    rfl
  Â· intro xâ‚€
    rw [basis.tendsto_iff basis]
    intro U U_in
    exact conj xâ‚€ U_in

end GroupFilterBasis

/-- A `RingFilterBasis` on a ring is a `FilterBasis` satisfying some additional axioms.
  Example : if `R` is a topological ring then the neighbourhoods of the identity are a
  `RingFilterBasis`. Conversely given a `RingFilterBasis` on a ring `R`, one can define a
  topology on `R` which is compatible with the ring structure. -/
class RingFilterBasis (R : Type u) [Ring R] extends AddGroupFilterBasis R where
  mul' : âˆ€ {U}, U âˆˆ sets â†’ âˆƒ V âˆˆ sets, V * V âŠ† U
  mul_left' : âˆ€ (xâ‚€ : R) {U}, U âˆˆ sets â†’ âˆƒ V âˆˆ sets, V âŠ† (fun x â†¦ xâ‚€ * x) â»Â¹' U
  mul_right' : âˆ€ (xâ‚€ : R) {U}, U âˆˆ sets â†’ âˆƒ V âˆˆ sets, V âŠ† (fun x â†¦ x * xâ‚€) â»Â¹' U

namespace RingFilterBasis

variable {R : Type u} [Ring R] (B : RingFilterBasis R)

instance : Membership (Set R) (RingFilterBasis R) :=
  âŸ¨fun B s â†¦ s âˆˆ B.setsâŸ©

theorem mul {U : Set R} (hU : U âˆˆ B) : âˆƒ V âˆˆ B, V * V âŠ† U :=
  mul' hU

theorem mul_left (xâ‚€ : R) {U : Set R} (hU : U âˆˆ B) : âˆƒ V âˆˆ B, V âŠ† (fun x â†¦ xâ‚€ * x) â»Â¹' U :=
  mul_left' xâ‚€ hU

theorem mul_right (xâ‚€ : R) {U : Set R} (hU : U âˆˆ B) : âˆƒ V âˆˆ B, V âŠ† (fun x â†¦ x * xâ‚€) â»Â¹' U :=
  mul_right' xâ‚€ hU

/-- The topology associated to a ring filter basis.
It has the given basis as a basis of neighborhoods of zero. -/
def topology : TopologicalSpace R :=
  B.toAddGroupFilterBasis.topology

/-- If a ring is endowed with a topological structure coming from
a ring filter basis then it's a topological ring. -/
instance (priority := 100) isTopologicalRing {R : Type u} [Ring R] (B : RingFilterBasis R) :
    @IsTopologicalRing R B.topology _ := by
  let B' := B.toAddGroupFilterBasis
  letI := B'.topology
  have basis := B'.nhds_zero_hasBasis
  have basis' := basis.prod basis
  haveI := B'.isTopologicalAddGroup
  apply IsTopologicalRing.of_addGroup_of_nhds_zero
  Â· rw [basis'.tendsto_iff basis]
    suffices âˆ€ U âˆˆ B', âˆƒ V W, (V âˆˆ B' âˆ§ W âˆˆ B') âˆ§ âˆ€ a b, a âˆˆ V â†’ b âˆˆ W â†’ a * b âˆˆ U by simpa
    intro U U_in
    rcases B.mul U_in with âŸ¨V, V_in, hVâŸ©
    refine âŸ¨V, V, âŸ¨V_in, V_inâŸ©, ?_âŸ©
    intro a b a_in b_in
    exact hV <| mul_mem_mul a_in b_in
  Â· intro xâ‚€
    rw [basis.tendsto_iff basis]
    intro U
    simpa using B.mul_left xâ‚€
  Â· intro xâ‚€
    rw [basis.tendsto_iff basis]
    intro U
    simpa using B.mul_right xâ‚€

end RingFilterBasis

/-- A `ModuleFilterBasis` on a module is a `FilterBasis` satisfying some additional axioms.
  Example : if `M` is a topological module then the neighbourhoods of zero are a
  `ModuleFilterBasis`. Conversely given a `ModuleFilterBasis` one can define a topology
  compatible with the module structure on `M`. -/
structure ModuleFilterBasis (R M : Type*) [CommRing R] [TopologicalSpace R] [AddCommGroup M]
  [Module R M] extends AddGroupFilterBasis M where
  smul' : âˆ€ {U}, U âˆˆ sets â†’ âˆƒ V âˆˆ ğ“ (0 : R), âˆƒ W âˆˆ sets, V â€¢ W âŠ† U
  smul_left' : âˆ€ (xâ‚€ : R) {U}, U âˆˆ sets â†’ âˆƒ V âˆˆ sets, V âŠ† (fun x â†¦ xâ‚€ â€¢ x) â»Â¹' U
  smul_right' : âˆ€ (mâ‚€ : M) {U}, U âˆˆ sets â†’ âˆ€á¶  x in ğ“ (0 : R), x â€¢ mâ‚€ âˆˆ U

namespace ModuleFilterBasis

variable {R M : Type*} [CommRing R] [TopologicalSpace R] [AddCommGroup M] [Module R M]
  (B : ModuleFilterBasis R M)

instance GroupFilterBasis.hasMem : Membership (Set M) (ModuleFilterBasis R M) :=
  âŸ¨fun B s â†¦ s âˆˆ B.setsâŸ©

theorem smul {U : Set M} (hU : U âˆˆ B) : âˆƒ V âˆˆ ğ“ (0 : R), âˆƒ W âˆˆ B, V â€¢ W âŠ† U :=
  B.smul' hU

theorem smul_left (xâ‚€ : R) {U : Set M} (hU : U âˆˆ B) : âˆƒ V âˆˆ B, V âŠ† (fun x â†¦ xâ‚€ â€¢ x) â»Â¹' U :=
  B.smul_left' xâ‚€ hU

theorem smul_right (mâ‚€ : M) {U : Set M} (hU : U âˆˆ B) : âˆ€á¶  x in ğ“ (0 : R), x â€¢ mâ‚€ âˆˆ U :=
  B.smul_right' mâ‚€ hU

/-- If `R` is discrete then the trivial additive group filter basis on any `R`-module is a
module filter basis. -/
instance [DiscreteTopology R] : Inhabited (ModuleFilterBasis R M) :=
  âŸ¨{
      show AddGroupFilterBasis M from
        default with
      smul' := by
        rintro U (rfl : U âˆˆ {{(0 : M)}})
        use univ, univ_mem, {0}, rfl
        rintro a âŸ¨x, -, m, rfl, rflâŸ©
        simp only [smul_zero, mem_singleton_iff]
      smul_left' := by
        rintro xâ‚€ U (h : U âˆˆ {{(0 : M)}})
        rw [mem_singleton_iff] at h
        use {0}, rfl
        simp [h]
      smul_right' := by
        rintro mâ‚€ U (h : U âˆˆ (0 : Set (Set M)))
        rw [Set.mem_zero] at h
        simp [h, nhds_discrete] }âŸ©

/-- The topology associated to a module filter basis on a module over a topological ring.
It has the given basis as a basis of neighborhoods of zero. -/
def topology : TopologicalSpace M :=
  B.toAddGroupFilterBasis.topology

/-- The topology associated to a module filter basis on a module over a topological ring.
It has the given basis as a basis of neighborhoods of zero. This version gets the ring
topology by unification instead of type class inference. -/
def topology' {R M : Type*} [CommRing R] {_ : TopologicalSpace R} [AddCommGroup M] [Module R M]
    (B : ModuleFilterBasis R M) : TopologicalSpace M :=
  B.toAddGroupFilterBasis.topology

/-- A topological add group with a basis of `ğ“ 0` satisfying the axioms of `ModuleFilterBasis`
is a topological module.

This lemma is mathematically useless because one could obtain such a result by applying
`ModuleFilterBasis.continuousSMul` and use the fact that group topologies are characterized
by their neighborhoods of 0 to obtain the `ContinuousSMul` on the pre-existing topology.

But it turns out it's just easier to get it as a byproduct of the proof, so this is just a free
quality-of-life improvement. -/
theorem _root_.ContinuousSMul.of_basis_zero {Î¹ : Type*} [IsTopologicalRing R] [TopologicalSpace M]
    [IsTopologicalAddGroup M] {p : Î¹ â†’ Prop} {b : Î¹ â†’ Set M} (h : HasBasis (ğ“ 0) p b)
    (hsmul : âˆ€ {i}, p i â†’ âˆƒ V âˆˆ ğ“ (0 : R), âˆƒ j, p j âˆ§ V â€¢ b j âŠ† b i)
    (hsmul_left : âˆ€ (xâ‚€ : R) {i}, p i â†’ âˆƒ j, p j âˆ§ MapsTo (xâ‚€ â€¢ Â·) (b j) (b i))
    (hsmul_right : âˆ€ (mâ‚€ : M) {i}, p i â†’ âˆ€á¶  x in ğ“ (0 : R), x â€¢ mâ‚€ âˆˆ b i) : ContinuousSMul R M := by
  apply ContinuousSMul.of_nhds_zero
  Â· rw [h.tendsto_right_iff]
    intro i hi
    rcases hsmul hi with âŸ¨V, V_in, j, hj, hVjâŸ©
    apply mem_of_superset (prod_mem_prod V_in <| h.mem_of_mem hj)
    rintro âŸ¨v, wâŸ© âŸ¨v_in : v âˆˆ V, w_in : w âˆˆ b jâŸ©
    exact hVj (Set.smul_mem_smul v_in w_in)
  Â· intro mâ‚€
    rw [h.tendsto_right_iff]
    intro i hi
    exact hsmul_right mâ‚€ hi
  Â· intro xâ‚€
    rw [h.tendsto_right_iff]
    intro i hi
    rcases hsmul_left xâ‚€ hi with âŸ¨j, hj, hjiâŸ©
    exact mem_of_superset (h.mem_of_mem hj) hji

/-- If a module is endowed with a topological structure coming from
a module filter basis then it's a topological module. -/
instance (priority := 100) continuousSMul [IsTopologicalRing R] :
    @ContinuousSMul R M _ _ B.topology := by
  let B' := B.toAddGroupFilterBasis
  let _ := B'.topology
  have _ := B'.isTopologicalAddGroup
  exact ContinuousSMul.of_basis_zero B'.nhds_zero_hasBasis
      (fun {_} => by simpa using B.smul)
      (by simpa using B.smul_left) B.smul_right

/-- Build a module filter basis from compatible ring and additive group filter bases. -/
def ofBases {R M : Type*} [CommRing R] [AddCommGroup M] [Module R M] (BR : RingFilterBasis R)
    (BM : AddGroupFilterBasis M) (smul : âˆ€ {U}, U âˆˆ BM â†’ âˆƒ V âˆˆ BR, âˆƒ W âˆˆ BM, V â€¢ W âŠ† U)
    (smul_left : âˆ€ (xâ‚€ : R) {U}, U âˆˆ BM â†’ âˆƒ V âˆˆ BM, V âŠ† (fun x â†¦ xâ‚€ â€¢ x) â»Â¹' U)
    (smul_right : âˆ€ (mâ‚€ : M) {U}, U âˆˆ BM â†’ âˆƒ V âˆˆ BR, V âŠ† (fun x â†¦ x â€¢ mâ‚€) â»Â¹' U) :
    @ModuleFilterBasis R M _ BR.topology _ _ :=
  let _ := BR.topology
  { BM with
    smul' := by
      intro U U_in
      rcases smul U_in with âŸ¨V, V_in, W, W_in, HâŸ©
      exact âŸ¨V, BR.toAddGroupFilterBasis.mem_nhds_zero V_in, W, W_in, HâŸ©
    smul_left' := smul_left
    smul_right' := by
      intro mâ‚€ U U_in
      rcases smul_right mâ‚€ U_in with âŸ¨V, V_in, HâŸ©
      exact mem_of_superset (BR.toAddGroupFilterBasis.mem_nhds_zero V_in) H }

end ModuleFilterBasis
