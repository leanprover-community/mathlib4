/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import Mathlib.Topology.UniformSpace.UniformConvergenceTopology
import Mathlib.Analysis.LocallyConvex.Bounded
import Mathlib.Topology.Algebra.FilterBasis

#align_import topology.algebra.uniform_convergence from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Algebraic facts about the topology of uniform convergence

This file contains algebraic compatibility results about the uniform structure of uniform
convergence / `ğ”–`-convergence. They will mostly be useful for defining strong topologies on the
space of continuous linear maps between two topological vector spaces.

## Main statements

* `UniformFun.uniform_group` : if `G` is a uniform group, then `Î± â†’áµ¤ G` a uniform group
* `UniformOnFun.uniform_group` : if `G` is a uniform group, then for any `ğ”– : Set (Set Î±)`,
  `Î± â†’áµ¤[ğ”–] G` a uniform group.
* `UniformOnFun.continuousSMul_induced_of_image_bounded` : let `E` be a TVS, `ğ”– : Set (Set Î±)` and
  `H` a submodule of `Î± â†’áµ¤[ğ”–] E`. If the image of any `S âˆˆ ğ”–` by any `u âˆˆ H` is bounded (in the
  sense of `Bornology.IsVonNBounded`), then `H`, equipped with the topology induced from
  `Î± â†’áµ¤[ğ”–] E`, is a TVS.

## Implementation notes

Like in `Topology/UniformSpace/UniformConvergenceTopology`, we use the type aliases
`UniformFun` (denoted `Î± â†’áµ¤ Î²`) and `UniformOnFun` (denoted `Î± â†’áµ¤[ğ”–] Î²`) for functions from `Î±`
to `Î²` endowed with the structures of uniform convergence and `ğ”–`-convergence.

## References

* [N. Bourbaki, *General Topology, Chapter X*][bourbaki1966]
* [N. Bourbaki, *Topological Vector Spaces*][bourbaki1987]

## Tags

uniform convergence, strong dual

-/

open Filter

open scoped Topology Pointwise UniformConvergence Uniformity

section AlgebraicInstances

variable {Î± Î² Î¹ R : Type*} {ğ”– : Set <| Set Î±} {x : Î±}

@[to_additive] instance [One Î²] : One (Î± â†’áµ¤ Î²) := Pi.instOne

@[to_additive (attr := simp)]
lemma UniformFun.toFun_one [One Î²] : toFun (1 : Î± â†’áµ¤ Î²) = 1 := rfl

@[to_additive (attr := simp)]
lemma UniformFun.ofFun_one [One Î²] : ofFun (1 : Î± â†’ Î²) = 1 := rfl

@[to_additive] instance [One Î²] : One (Î± â†’áµ¤[ğ”–] Î²) := Pi.instOne

@[to_additive (attr := simp)]
lemma UniformOnFun.toFun_one [One Î²] : toFun ğ”– (1 : Î± â†’áµ¤[ğ”–] Î²) = 1 := rfl

@[to_additive (attr := simp)]
lemma UniformOnFun.one_apply [One Î²] : ofFun ğ”– (1 : Î± â†’ Î²) = 1 := rfl

@[to_additive] instance [Mul Î²] : Mul (Î± â†’áµ¤ Î²) := Pi.instMul

@[to_additive (attr := simp)]
lemma UniformFun.toFun_mul [Mul Î²] (f g : Î± â†’áµ¤ Î²) : toFun (f * g) = toFun f * toFun g := rfl

@[to_additive (attr := simp)]
lemma UniformFun.ofFun_mul [Mul Î²] (f g : Î± â†’ Î²) : ofFun (f * g) = ofFun f * ofFun g := rfl

@[to_additive] instance [Mul Î²] : Mul (Î± â†’áµ¤[ğ”–] Î²) := Pi.instMul

@[to_additive (attr := simp)]
lemma UniformOnFun.toFun_mul [Mul Î²] (f g : Î± â†’áµ¤[ğ”–] Î²) :
    toFun ğ”– (f * g) = toFun ğ”– f * toFun ğ”– g :=
  rfl

@[to_additive (attr := simp)]
lemma UniformOnFun.ofFun_mul [Mul Î²] (f g : Î± â†’ Î²) : ofFun ğ”– (f * g) = ofFun ğ”– f * ofFun ğ”– g := rfl

@[to_additive] instance [Inv Î²] : Inv (Î± â†’áµ¤ Î²) := Pi.instInv

@[to_additive (attr := simp)]
lemma UniformFun.toFun_inv [Inv Î²] (f : Î± â†’áµ¤ Î²) : toFun (fâ»Â¹) = (toFun f)â»Â¹ := rfl

@[to_additive (attr := simp)]
lemma UniformFun.ofFun_inv [Inv Î²] (f : Î± â†’ Î²) : ofFun (fâ»Â¹) = (ofFun f)â»Â¹ := rfl

@[to_additive] instance [Inv Î²] : Inv (Î± â†’áµ¤[ğ”–] Î²) := Pi.instInv

@[to_additive (attr := simp)]
lemma UniformOnFun.toFun_inv [Inv Î²] (f : Î± â†’áµ¤[ğ”–] Î²) : toFun ğ”– (fâ»Â¹) = (toFun ğ”– f)â»Â¹ := rfl

@[to_additive (attr := simp)]
lemma UniformOnFun.ofFun_inv [Inv Î²] (f : Î± â†’ Î²) : ofFun ğ”– (fâ»Â¹) = (ofFun ğ”– f)â»Â¹ := rfl

@[to_additive] instance [Div Î²] : Div (Î± â†’áµ¤ Î²) := Pi.instDiv

@[to_additive (attr := simp)]
lemma UniformFun.toFun_div [Div Î²] (f g : Î± â†’áµ¤ Î²) : toFun (f / g) = toFun f / toFun g := rfl

@[to_additive (attr := simp)]
lemma UniformFun.ofFun_div [Div Î²] (f g : Î± â†’ Î²) : ofFun (f / g) = ofFun f / ofFun g := rfl

@[to_additive] instance [Div Î²] : Div (Î± â†’áµ¤[ğ”–] Î²) := Pi.instDiv

@[to_additive (attr := simp)]
lemma UniformOnFun.toFun_div [Div Î²] (f g : Î± â†’áµ¤[ğ”–] Î²) :
    toFun ğ”– (f / g) = toFun ğ”– f / toFun ğ”– g :=
  rfl

@[to_additive (attr := simp)]
lemma UniformOnFun.ofFun_div [Div Î²] (f g : Î± â†’ Î²) : ofFun ğ”– (f / g) = ofFun ğ”– f / ofFun ğ”– g := rfl

@[to_additive]
instance [Monoid Î²] : Monoid (Î± â†’áµ¤ Î²) :=
  Pi.monoid

@[to_additive]
instance [Monoid Î²] : Monoid (Î± â†’áµ¤[ğ”–] Î²) :=
  Pi.monoid

@[to_additive]
instance [CommMonoid Î²] : CommMonoid (Î± â†’áµ¤ Î²) :=
  Pi.commMonoid

@[to_additive]
instance [CommMonoid Î²] : CommMonoid (Î± â†’áµ¤[ğ”–] Î²) :=
  Pi.commMonoid

@[to_additive]
instance [Group Î²] : Group (Î± â†’áµ¤ Î²) :=
  Pi.group

@[to_additive]
instance [Group Î²] : Group (Î± â†’áµ¤[ğ”–] Î²) :=
  Pi.group

@[to_additive]
instance [CommGroup Î²] : CommGroup (Î± â†’áµ¤ Î²) :=
  Pi.commGroup

@[to_additive]
instance [CommGroup Î²] : CommGroup (Î± â†’áµ¤[ğ”–] Î²) :=
  Pi.commGroup

instance {M : Type*} [SMul M Î²] : SMul M (Î± â†’áµ¤ Î²) := Pi.instSMul

@[simp]
lemma UniformFun.toFun_smul {M : Type*} [SMul M Î²] (c : M) (f : Î± â†’áµ¤ Î²) :
    toFun (c â€¢ f) = c â€¢ toFun f :=
  rfl

@[simp]
lemma UniformFun.ofFun_smul {M : Type*} [SMul M Î²] (c : M) (f : Î± â†’ Î²) :
    ofFun (c â€¢ f) = c â€¢ ofFun f :=
  rfl

instance {M : Type*} [SMul M Î²] : SMul M (Î± â†’áµ¤[ğ”–] Î²) := Pi.instSMul

@[simp]
lemma UniformOnFun.toFun_smul {M : Type*} [SMul M Î²] (c : M) (f : Î± â†’áµ¤[ğ”–] Î²) :
    toFun ğ”– (c â€¢ f) = c â€¢ toFun ğ”– f :=
  rfl

@[simp]
lemma UniformOnFun.ofFun_smul {M : Type*} [SMul M Î²] (c : M) (f : Î± â†’ Î²) :
    ofFun ğ”– (c â€¢ f) = c â€¢ ofFun ğ”– f :=
  rfl

instance {M N : Type*} [SMul M N] [SMul M Î²] [SMul N Î²] [IsScalarTower M N Î²] :
    IsScalarTower M N (Î± â†’áµ¤ Î²) :=
  Pi.isScalarTower

instance {M N : Type*} [SMul M N] [SMul M Î²] [SMul N Î²] [IsScalarTower M N Î²] :
    IsScalarTower M N (Î± â†’áµ¤[ğ”–] Î²) :=
  Pi.isScalarTower

instance {M N : Type*} [SMul M Î²] [SMul N Î²] [SMulCommClass M N Î²] :
    SMulCommClass M N (Î± â†’áµ¤ Î²) :=
  Pi.smulCommClass

instance {M N : Type*} [SMul M Î²] [SMul N Î²] [SMulCommClass M N Î²] :
    SMulCommClass M N (Î± â†’áµ¤[ğ”–] Î²) :=
  Pi.smulCommClass

instance {M : Type*} [Monoid M] [MulAction M Î²] : MulAction M (Î± â†’áµ¤ Î²) := Pi.mulAction _

instance {M : Type*} [Monoid M] [MulAction M Î²] : MulAction M (Î± â†’áµ¤[ğ”–] Î²) := Pi.mulAction _

instance {M : Type*} [Monoid M] [AddMonoid Î²] [DistribMulAction M Î²] :
    DistribMulAction M (Î± â†’áµ¤ Î²) :=
  Pi.distribMulAction _

instance {M : Type*} [Monoid M] [AddMonoid Î²] [DistribMulAction M Î²] :
    DistribMulAction M (Î± â†’áµ¤[ğ”–] Î²) :=
  Pi.distribMulAction _

instance [Semiring R] [AddCommMonoid Î²] [Module R Î²] : Module R (Î± â†’áµ¤ Î²) :=
  Pi.module _ _ _

instance [Semiring R] [AddCommMonoid Î²] [Module R Î²] : Module R (Î± â†’áµ¤[ğ”–] Î²) :=
  Pi.module _ _ _

end AlgebraicInstances

section Group

variable {Î± G Î¹ : Type*} [Group G] {ğ”– : Set <| Set Î±} [UniformSpace G] [UniformGroup G]

/-- If `G` is a uniform group, then `Î± â†’áµ¤ G` is a uniform group as well. -/
@[to_additive "If `G` is a uniform additive group,
then `Î± â†’áµ¤ G` is a uniform additive group as well."]
instance : UniformGroup (Î± â†’áµ¤ G) :=
  âŸ¨(-- Since `(/) : G Ã— G â†’ G` is uniformly continuous,
    -- `UniformFun.postcomp_uniformContinuous` tells us that
    -- `((/) âˆ˜ â€”) : (Î± â†’áµ¤ G Ã— G) â†’ (Î± â†’áµ¤ G)` is uniformly continuous too. By precomposing with
    -- `UniformFun.uniformEquivProdArrow`, this gives that
    -- `(/) : (Î± â†’áµ¤ G) Ã— (Î± â†’áµ¤ G) â†’ (Î± â†’áµ¤ G)` is also uniformly continuous
    UniformFun.postcomp_uniformContinuous uniformContinuous_div).comp
    UniformFun.uniformEquivProdArrow.symm.uniformContinuousâŸ©

@[to_additive]
protected theorem UniformFun.hasBasis_nhds_one_of_basis {p : Î¹ â†’ Prop} {b : Î¹ â†’ Set G}
    (h : (ğ“ 1 : Filter G).HasBasis p b) :
    (ğ“ 1 : Filter (Î± â†’áµ¤ G)).HasBasis p fun i => { f : Î± â†’áµ¤ G | âˆ€ x, toFun f x âˆˆ b i } := by
  have := h.comap fun p : G Ã— G => p.2 / p.1
  rw [â† uniformity_eq_comap_nhds_one] at this
  convert UniformFun.hasBasis_nhds_of_basis Î± _ (1 : Î± â†’áµ¤ G) this
  -- Porting note: removed `ext i f` here, as it has already been done by `convert`.
  simp
#align uniform_fun.has_basis_nhds_one_of_basis UniformFun.hasBasis_nhds_one_of_basis
#align uniform_fun.has_basis_nhds_zero_of_basis UniformFun.hasBasis_nhds_zero_of_basis

@[to_additive]
protected theorem UniformFun.hasBasis_nhds_one :
    (ğ“ 1 : Filter (Î± â†’áµ¤ G)).HasBasis (fun V : Set G => V âˆˆ (ğ“ 1 : Filter G)) fun V =>
      { f : Î± â†’ G | âˆ€ x, f x âˆˆ V } :=
  UniformFun.hasBasis_nhds_one_of_basis (basis_sets _)
#align uniform_fun.has_basis_nhds_one UniformFun.hasBasis_nhds_one
#align uniform_fun.has_basis_nhds_zero UniformFun.hasBasis_nhds_zero

/-- Let `ğ”– : Set (Set Î±)`. If `G` is a uniform group, then `Î± â†’áµ¤[ğ”–] G` is a uniform group as
well. -/
@[to_additive "Let `ğ”– : Set (Set Î±)`. If `G` is a uniform additive group,
then `Î± â†’áµ¤[ğ”–] G` is a uniform additive group as well."]
instance : UniformGroup (Î± â†’áµ¤[ğ”–] G) :=
  âŸ¨(-- Since `(/) : G Ã— G â†’ G` is uniformly continuous,
    -- `UniformOnFun.postcomp_uniformContinuous` tells us that
    -- `((/) âˆ˜ â€”) : (Î± â†’áµ¤[ğ”–] G Ã— G) â†’ (Î± â†’áµ¤[ğ”–] G)` is uniformly continuous too. By precomposing with
    -- `UniformOnFun.uniformEquivProdArrow`, this gives that
    -- `(/) : (Î± â†’áµ¤[ğ”–] G) Ã— (Î± â†’áµ¤[ğ”–] G) â†’ (Î± â†’áµ¤[ğ”–] G)` is also uniformly continuous
    UniformOnFun.postcomp_uniformContinuous uniformContinuous_div).comp
    UniformOnFun.uniformEquivProdArrow.symm.uniformContinuousâŸ©

@[to_additive]
protected theorem UniformOnFun.hasBasis_nhds_one_of_basis (ğ”– : Set <| Set Î±) (hğ”–â‚ : ğ”–.Nonempty)
    (hğ”–â‚‚ : DirectedOn (Â· âŠ† Â·) ğ”–) {p : Î¹ â†’ Prop} {b : Î¹ â†’ Set G}
    (h : (ğ“ 1 : Filter G).HasBasis p b) :
    (ğ“ 1 : Filter (Î± â†’áµ¤[ğ”–] G)).HasBasis (fun Si : Set Î± Ã— Î¹ => Si.1 âˆˆ ğ”– âˆ§ p Si.2) fun Si =>
      { f : Î± â†’áµ¤[ğ”–] G | âˆ€ x âˆˆ Si.1, toFun ğ”– f x âˆˆ b Si.2 } := by
  have := h.comap fun p : G Ã— G => p.1 / p.2
  rw [â† uniformity_eq_comap_nhds_one_swapped] at this
  convert UniformOnFun.hasBasis_nhds_of_basis Î± _ ğ”– (1 : Î± â†’áµ¤[ğ”–] G) hğ”–â‚ hğ”–â‚‚ this
  -- Porting note: removed `ext i f` here, as it has already been done by `convert`.
  simp [UniformOnFun.gen]
#align uniform_on_fun.has_basis_nhds_one_of_basis UniformOnFun.hasBasis_nhds_one_of_basis
#align uniform_on_fun.has_basis_nhds_zero_of_basis UniformOnFun.hasBasis_nhds_zero_of_basis

@[to_additive]
protected theorem UniformOnFun.hasBasis_nhds_one (ğ”– : Set <| Set Î±) (hğ”–â‚ : ğ”–.Nonempty)
    (hğ”–â‚‚ : DirectedOn (Â· âŠ† Â·) ğ”–) :
    (ğ“ 1 : Filter (Î± â†’áµ¤[ğ”–] G)).HasBasis
      (fun SV : Set Î± Ã— Set G => SV.1 âˆˆ ğ”– âˆ§ SV.2 âˆˆ (ğ“ 1 : Filter G)) fun SV =>
      { f : Î± â†’áµ¤[ğ”–] G | âˆ€ x âˆˆ SV.1, f x âˆˆ SV.2 } :=
  UniformOnFun.hasBasis_nhds_one_of_basis ğ”– hğ”–â‚ hğ”–â‚‚ (basis_sets _)
#align uniform_on_fun.has_basis_nhds_one UniformOnFun.hasBasis_nhds_one
#align uniform_on_fun.has_basis_nhds_zero UniformOnFun.hasBasis_nhds_zero

end Group

section ConstSMul

variable (M Î± X : Type*) [SMul M X] [UniformSpace X] [UniformContinuousConstSMul M X]

instance UniformFun.uniformContinuousConstSMul :
    UniformContinuousConstSMul M (Î± â†’áµ¤ X) where
  uniformContinuous_const_smul c := UniformFun.postcomp_uniformContinuous <|
    uniformContinuous_const_smul c

instance UniformFunOn.uniformContinuousConstSMul {ğ”– : Set (Set Î±)} :
    UniformContinuousConstSMul M (Î± â†’áµ¤[ğ”–] X) where
  uniformContinuous_const_smul c := UniformOnFun.postcomp_uniformContinuous <|
    uniformContinuous_const_smul c

end ConstSMul

section Module

variable (ğ•œ Î± E H : Type*) {hom : Type*} [NormedField ğ•œ] [AddCommGroup H] [Module ğ•œ H]
  [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace H] [UniformSpace E] [UniformAddGroup E]
  [ContinuousSMul ğ•œ E] {ğ”– : Set <| Set Î±}
  [FunLike hom H (Î± â†’ E)] [LinearMapClass hom ğ•œ H (Î± â†’ E)]

/-- Let `E` be a topological vector space over a normed field `ğ•œ`, let `Î±` be any type.
Let `H` be a submodule of `Î± â†’áµ¤ E` such that the range of each `f âˆˆ H` is von Neumann bounded.
Then `H` is a topological vector space over `ğ•œ`,
i.e., the pointwise scalar multiplication is continuous in both variables.

For convenience we require that `H` is a vector space over `ğ•œ`
with a topology induced by `UniformFun.ofFun âˆ˜ Ï†`, where `Ï† : H â†’â‚—[ğ•œ] (Î± â†’ E)`. -/
lemma UniformFun.continuousSMul_induced_of_range_bounded (Ï† : hom)
    (hÏ† : Inducing (ofFun âˆ˜ Ï†)) (h : âˆ€ u : H, Bornology.IsVonNBounded ğ•œ (Set.range (Ï† u))) :
    ContinuousSMul ğ•œ H := by
  have : TopologicalAddGroup H :=
    let ofFun' : (Î± â†’ E) â†’+ (Î± â†’áµ¤ E) := AddMonoidHom.id _
    Inducing.topologicalAddGroup (ofFun'.comp (Ï† : H â†’+ (Î± â†’ E))) hÏ†
  have hb : (ğ“ (0 : H)).HasBasis (Â· âˆˆ ğ“ (0 : E)) fun V â†¦ {u | âˆ€ x, Ï† u x âˆˆ V} := by
    simp only [hÏ†.nhds_eq_comap, Function.comp_apply, map_zero]
    exact UniformFun.hasBasis_nhds_zero.comap _
  apply ContinuousSMul.of_basis_zero hb
  Â· intro U hU
    have : Tendsto (fun x : ğ•œ Ã— E â†¦ x.1 â€¢ x.2) (ğ“ 0) (ğ“ 0) :=
      continuous_smul.tendsto' _ _ (zero_smul _ _)
    rcases ((Filter.basis_sets _).prod_nhds (Filter.basis_sets _)).tendsto_left_iff.1 this U hU
      with âŸ¨âŸ¨V, WâŸ©, âŸ¨hV, hWâŸ©, hVWâŸ©
    refine âŸ¨V, hV, W, hW, Set.smul_subset_iff.2 fun a ha u hu x â†¦ ?_âŸ©
    rw [map_smul]
    exact hVW (Set.mk_mem_prod ha (hu x))
  Â· intro c U hU
    have : Tendsto (c â€¢ Â· : E â†’ E) (ğ“ 0) (ğ“ 0) :=
      (continuous_const_smul c).tendsto' _ _ (smul_zero _)
    refine âŸ¨_, this hU, fun u hu x â†¦ ?_âŸ©
    simpa only [map_smul] using hu x
  Â· intro u U hU
    simp only [Set.mem_setOf_eq, map_smul, Pi.smul_apply]
    simpa only [Set.mapsTo_range_iff] using (h u hU).eventually_nhds_zero (mem_of_mem_nhds hU)

/-- Let `E` be a TVS, `ğ”– : Set (Set Î±)` and `H` a submodule of `Î± â†’áµ¤[ğ”–] E`. If the image of any
`S âˆˆ ğ”–` by any `u âˆˆ H` is bounded (in the sense of `Bornology.IsVonNBounded`), then `H`,
equipped with the topology of `ğ”–`-convergence, is a TVS.

For convenience, we don't literally ask for `H : Submodule (Î± â†’áµ¤[ğ”–] E)`. Instead, we prove the
result for any vector space `H` equipped with a linear inducing to `Î± â†’áµ¤[ğ”–] E`, which is often
easier to use. We also state the `Submodule` version as
`UniformOnFun.continuousSMul_submodule_of_image_bounded`. -/
theorem UniformOnFun.continuousSMul_induced_of_image_bounded (Ï† : hom) (hÏ† : Inducing (ofFun ğ”– âˆ˜ Ï†))
    (h : âˆ€ u : H, âˆ€ s âˆˆ ğ”–, Bornology.IsVonNBounded ğ•œ ((Ï† u : Î± â†’ E) '' s)) :
    ContinuousSMul ğ•œ H := by
  obtain rfl := hÏ†.induced; clear hÏ†
  simp only [induced_iInf, UniformOnFun.topologicalSpace_eq, induced_compose]
  refine continuousSMul_iInf fun s â†¦ continuousSMul_iInf fun hs â†¦ ?_
  letI : TopologicalSpace H :=
    .induced (UniformFun.ofFun âˆ˜ s.restrict âˆ˜ Ï†) (UniformFun.topologicalSpace s E)
  set Ï†' : H â†’â‚—[ğ•œ] (s â†’ E) :=
    { toFun := s.restrict âˆ˜ Ï†,
      map_smul' := fun c x â†¦ by exact congr_arg s.restrict (map_smul Ï† c x),
      map_add' := fun x y â†¦ by exact congr_arg s.restrict (map_add Ï† x y) }
  refine UniformFun.continuousSMul_induced_of_range_bounded ğ•œ s E H Ï†' âŸ¨rflâŸ© fun u â†¦ ?_
  simpa only [Set.image_eq_range] using h u s hs
#align uniform_on_fun.has_continuous_smul_induced_of_image_bounded UniformOnFun.continuousSMul_induced_of_image_bounded

/-- Let `E` be a TVS, `ğ”– : Set (Set Î±)` and `H` a submodule of `Î± â†’áµ¤[ğ”–] E`. If the image of any
`S âˆˆ ğ”–` by any `u âˆˆ H` is bounded (in the sense of `Bornology.IsVonNBounded`), then `H`,
equipped with the topology of `ğ”–`-convergence, is a TVS.

If you have a hard time using this lemma, try the one above instead. -/
theorem UniformOnFun.continuousSMul_submodule_of_image_bounded (H : Submodule ğ•œ (Î± â†’áµ¤[ğ”–] E))
    (h : âˆ€ u âˆˆ H, âˆ€ s âˆˆ ğ”–, Bornology.IsVonNBounded ğ•œ (u '' s)) :
    @ContinuousSMul ğ•œ H _ _ ((UniformOnFun.topologicalSpace Î± E ğ”–).induced ((â†‘) : H â†’ Î± â†’áµ¤[ğ”–] E)) :=
  UniformOnFun.continuousSMul_induced_of_image_bounded ğ•œ Î± E H
    (LinearMap.id.domRestrict H : H â†’â‚—[ğ•œ] Î± â†’ E) inducing_subtype_val fun âŸ¨u, huâŸ© => h u hu
#align uniform_on_fun.has_continuous_smul_submodule_of_image_bounded UniformOnFun.continuousSMul_submodule_of_image_bounded

end Module
