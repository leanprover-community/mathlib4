/-
Copyright (c) 2024 Anatole Dedeker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedeker, Etienne Marion, Florestan Martin-Baillon, Vincent Guirardel
-/
module

public import Mathlib.Topology.Algebra.Group.Quotient
public import Mathlib.Topology.Algebra.MulAction
public import Mathlib.Topology.Algebra.Group.Defs
public import Mathlib.Topology.LocalAtTarget

/-!
# Proper group action

In this file we define proper action of a group on a topological space, and we prove that in this
case the quotient space is T2. We also give equivalent definitions of proper action using
ultrafilters and show the transfer of proper action to a closed subgroup.

## Main definitions

* `ProperSMul` : a group `G` acts properly on a topological space `X`
  if the map `(g, x) ‚Ü¶ (g ‚Ä¢ x, x)` is proper, in the sense of `IsProperMap`.

## Main statements

* `t2Space_quotient_mulAction_of_properSMul`: If a group `G` acts properly
  on a topological space `X`, then the quotient space is Hausdorff (T2).
* `t2Space_of_properSMul_of_t1Group`: If a T1 group acts properly on a topological space,
  then this topological space is T2.

## References

* [N. Bourbaki, *General Topology*][bourbaki1966]

## Tags

Hausdorff, group action, proper action
-/

@[expose] public section

open Filter Topology Set Prod

/-- Proper group action in the sense of Bourbaki:
the map `G √ó X ‚Üí X √ó X` is a proper map (see `IsProperMap`). -/
class ProperVAdd (G X : Type*) [TopologicalSpace G] [TopologicalSpace X] [AddGroup G]
    [AddAction G X] : Prop where
  /-- Proper group action in the sense of Bourbaki:
  the map `G √ó X ‚Üí X √ó X` is a proper map (see `IsProperMap`). -/
  isProperMap_vadd_pair : IsProperMap (fun gx ‚Ü¶ (gx.1 +·µ• gx.2, gx.2) : G √ó X ‚Üí X √ó X)

/-- Proper group action in the sense of Bourbaki:
the map `G √ó X ‚Üí X √ó X` is a proper map (see `IsProperMap`). -/
@[to_additive existing (attr := mk_iff)]
class ProperSMul (G X : Type*) [TopologicalSpace G] [TopologicalSpace X] [Group G]
    [MulAction G X] : Prop where
  /-- Proper group action in the sense of Bourbaki:
  the map `G √ó X ‚Üí X √ó X` is a proper map (see `IsProperMap`). -/
  isProperMap_smul_pair : IsProperMap (fun gx ‚Ü¶ (gx.1 ‚Ä¢ gx.2, gx.2) : G √ó X ‚Üí X √ó X)

attribute [to_additive existing] properSMul_iff

variable {G X : Type*} [Group G] [MulAction G X]
variable [TopologicalSpace G] [TopologicalSpace X]

/-- If a group acts properly then in particular it acts continuously. -/
@[to_additive /-- If a group acts properly then in particular it acts continuously. -/]
-- See note [lower instance property]
instance (priority := 100) ProperSMul.toContinuousSMul [ProperSMul G X] : ContinuousSMul G X where
  continuous_smul := isProperMap_smul_pair.continuous.fst

/-- A group `G` acts properly on a topological space `X` if and only if for all ultrafilters
`ùí∞` on `X √ó G`, if `ùí∞` converges to `(x‚ÇÅ, x‚ÇÇ)` along the map `(g, x) ‚Ü¶ (g ‚Ä¢ x, x)`,
then there exists `g : G` such that `g ‚Ä¢ x‚ÇÇ = x‚ÇÅ` and `ùí∞.fst` converges to `g`. -/
@[to_additive /-- An additive group `G` acts properly on a topological space `X` if and only if
for all ultrafilters `ùí∞` on `X`, if `ùí∞` converges to `(x‚ÇÅ, x‚ÇÇ)`
along the map `(g, x) ‚Ü¶ (g ‚Ä¢ x, x)`, then there exists `g : G` such that `g ‚Ä¢ x‚ÇÇ = x‚ÇÅ`
and `ùí∞.fst` converges to `g`. -/]
theorem properSMul_iff_continuousSMul_ultrafilter_tendsto :
    ProperSMul G X ‚Üî ContinuousSMul G X ‚àß
      (‚àÄ ùí∞ : Ultrafilter (G √ó X), ‚àÄ x‚ÇÅ x‚ÇÇ : X,
        Tendsto (fun gx : G √ó X ‚Ü¶ (gx.1 ‚Ä¢ gx.2, gx.2)) ùí∞ (ùìù (x‚ÇÅ, x‚ÇÇ)) ‚Üí
        ‚àÉ g : G, g ‚Ä¢ x‚ÇÇ = x‚ÇÅ ‚àß Tendsto (Prod.fst : G √ó X ‚Üí G) ùí∞ (ùìù g)) := by
  refine ‚ü®fun h ‚Ü¶ ‚ü®inferInstance, fun ùí∞ x‚ÇÅ x‚ÇÇ h' ‚Ü¶ ?_‚ü©, fun ‚ü®cont, h‚ü© ‚Ü¶ ?_‚ü©
  ¬∑ rw [properSMul_iff, isProperMap_iff_ultrafilter] at h
    rcases h.2 h' with ‚ü®gx, hgx1, hgx2‚ü©
    refine ‚ü®gx.1, ?_, (continuous_fst.tendsto gx).mono_left hgx2‚ü©
    simp only [Prod.mk.injEq] at hgx1
    rw [‚Üê hgx1.2, hgx1.1]
  ¬∑ rw [properSMul_iff, isProperMap_iff_ultrafilter]
    refine ‚ü®by fun_prop, fun ùí∞ (x‚ÇÅ, x‚ÇÇ) hxx ‚Ü¶ ?_‚ü©
    rcases h ùí∞ x‚ÇÅ x‚ÇÇ hxx with ‚ü®g, hg1, hg2‚ü©
    refine ‚ü®(g, x‚ÇÇ), by simp_rw [hg1], ?_‚ü©
    rw [nhds_prod_eq, ùí∞.le_prod]
    exact ‚ü®hg2, (continuous_snd.tendsto _).comp hxx‚ü©

/-- A group `G` acts properly on a T2 topological space `X` if and only if for all ultrafilters
`ùí∞` on `X √ó G`, if `ùí∞` converges to `(x‚ÇÅ, x‚ÇÇ)` along the map `(g, x) ‚Ü¶ (g ‚Ä¢ x, x)`,
then there exists `g : G` such that `ùí∞.fst` converges to `g`. -/
theorem properSMul_iff_continuousSMul_ultrafilter_tendsto_t2 [T2Space X] :
    ProperSMul G X ‚Üî ContinuousSMul G X ‚àß
      (‚àÄ ùí∞ : Ultrafilter (G √ó X), ‚àÄ x‚ÇÅ x‚ÇÇ : X,
        Tendsto (fun gx : G √ó X ‚Ü¶ (gx.1 ‚Ä¢ gx.2, gx.2)) ùí∞ (ùìù (x‚ÇÅ, x‚ÇÇ)) ‚Üí
        ‚àÉ g : G, Tendsto (Prod.fst : G √ó X ‚Üí G) ùí∞ (ùìù g)) := by
  rw [properSMul_iff_continuousSMul_ultrafilter_tendsto]
  refine and_congr_right fun hc ‚Ü¶ ?_
  congrm ‚àÄ ùí∞ x‚ÇÅ x‚ÇÇ hxx, ‚àÉ g, ?_
  exact and_iff_right_of_imp fun hg ‚Ü¶ tendsto_nhds_unique
    (hg.smul ((continuous_snd.tendsto _).comp hxx)) ((continuous_fst.tendsto _).comp hxx)

/-- If `G` acts properly on `X`, then the quotient space is Hausdorff (T2). -/
@[to_additive /-- If `G` acts properly on `X`, then the quotient space is Hausdorff (T2). -/]
instance t2Space_quotient_mulAction_of_properSMul [ProperSMul G X] :
    T2Space (Quotient (MulAction.orbitRel G X)) := by
  rw [t2_iff_isClosed_diagonal]
  set R := MulAction.orbitRel G X
  let œÄ : X ‚Üí Quotient R := Quotient.mk'
  have : IsOpenQuotientMap (Prod.map œÄ œÄ) :=
    MulAction.isOpenQuotientMap_quotientMk.prodMap MulAction.isOpenQuotientMap_quotientMk
  rw [‚Üê this.isQuotientMap.isClosed_preimage]
  convert ProperSMul.isProperMap_smul_pair.isClosedMap.isClosed_range
  ¬∑ ext ‚ü®x‚ÇÅ, x‚ÇÇ‚ü©
    simp only [mem_preimage, map_apply, mem_diagonal_iff, mem_range, Prod.mk.injEq, Prod.exists,
      exists_eq_right]
    rw [Quotient.eq', MulAction.orbitRel_apply, MulAction.mem_orbit_iff]
  all_goals infer_instance

/-- If a T1 group acts properly on a topological space, then this topological space is T2. -/
@[to_additive /-- If a T1 group acts properly on a topological space,
then this topological space is T2. -/]
theorem t2Space_of_properSMul_of_t1Group [h_proper : ProperSMul G X] [T1Space G] : T2Space X := by
  let f := fun x : X ‚Ü¶ ((1 : G), x)
  have proper_f : IsProperMap f := by
    refine IsClosedEmbedding.isProperMap ‚ü®?_, ?_‚ü©
    ¬∑ let g := fun gx : G √ó X ‚Ü¶ gx.2
      have : Function.LeftInverse g f := fun x ‚Ü¶ by simp [f, g]
      exact this.isEmbedding (by fun_prop) (by fun_prop)
    ¬∑ have : range f = ({1} √óÀ¢ univ) := by simp [f, Set.singleton_prod]
      rw [this]
      exact isClosed_singleton.prod isClosed_univ
  rw [t2_iff_isClosed_diagonal]
  let g := fun gx : G √ó X ‚Ü¶ (gx.1 ‚Ä¢ gx.2, gx.2)
  have proper_g : IsProperMap g := (properSMul_iff G X).1 h_proper
  have : g ‚àò f = fun x ‚Ü¶ (x, x) := by ext x <;> simp [f, g]
  have range_gf : range (g ‚àò f) = diagonal X := by simp [this]
  rw [‚Üê range_gf]
  exact (proper_g.comp proper_f).isClosed_range

/-- If two groups `H` and `G` act on a topological space `X` such that `G` acts properly and
there exists a group homomorphism `H ‚Üí G` which is a closed embedding compatible with the actions,
then `H` also acts properly on `X`. -/
@[to_additive /-- If two groups `H` and `G` act on a topological space `X` such that `G` acts
properly and there exists a group homomorphism `H ‚Üí G` which is a closed embedding compatible with
the actions, then `H` also acts properly on `X`. -/]
theorem properSMul_of_isClosedEmbedding {H : Type*} [Group H] [MulAction H X] [TopologicalSpace H]
    [ProperSMul G X] (f : H ‚Üí* G) (f_clemb : IsClosedEmbedding f)
    (f_compat : ‚àÄ (h : H) (x : X), f h ‚Ä¢ x = h ‚Ä¢ x) : ProperSMul H X where
  isProperMap_smul_pair := by
    have h : IsProperMap (Prod.map f (fun x : X ‚Ü¶ x)) := f_clemb.isProperMap.prodMap isProperMap_id
    have : (fun hx : H √ó X ‚Ü¶ (hx.1 ‚Ä¢ hx.2, hx.2)) = (fun hx ‚Ü¶ (f hx.1 ‚Ä¢ hx.2, hx.2)) := by
      simp [f_compat]
    rw [this]
    exact ProperSMul.isProperMap_smul_pair.comp h

/-- If `H` is a closed subgroup of `G` and `G` acts properly on `X`, then so does `H`. -/
@[to_additive
/-- If `H` is a closed subgroup of `G` and `G` acts properly on `X`, then so does `H`. -/]
instance {H : Subgroup G} [ProperSMul G X] [H_closed : IsClosed (H : Set G)] : ProperSMul H X :=
  properSMul_of_isClosedEmbedding H.subtype H_closed.isClosedEmbedding_subtypeVal fun _ _ ‚Ü¶ rfl

/-- The action `G ‚Ü∑ G` by left translations is proper. -/
@[to_additive
/-- The action `G ‚Ü∑ G` by left translations is proper. -/]
instance [IsTopologicalGroup G] : ProperSMul G G where
  isProperMap_smul_pair := by
    let Œ¶ : G √ó G ‚âÉ‚Çú G √ó G :=
    { toFun := fun gh ‚Ü¶ (gh.1 * gh.2, gh.2)
      invFun := fun gh ‚Ü¶ (gh.1 * gh.2‚Åª¬π, gh.2)
      left_inv := fun _ ‚Ü¶ by simp
      right_inv := fun _ ‚Ü¶ by simp }
    exact Œ¶.isProperMap

open MulOpposite in
/-- The action `G·µê·µí·µñ ‚Ü∑ G` by right translations is proper. -/
@[to_additive
/-- The action `G·µÉ·µí·µñ ‚Ü∑ G` by right translations is proper. -/]
instance [IsTopologicalGroup G] : ProperSMul G·µê·µí·µñ G where
  isProperMap_smul_pair := by
    let Œ¶ : G·µê·µí·µñ √ó G ‚âÉ‚Çú G √ó G :=
    { toFun := fun gh ‚Ü¶ (gh.2 * (unop gh.1), gh.2)
      invFun := fun gh ‚Ü¶ (op (gh.2‚Åª¬π * gh.1), gh.2)
      left_inv := fun _ ‚Ü¶ by simp
      right_inv := fun _ ‚Ü¶ by simp }
    exact Œ¶.isProperMap

/-- Given a closed subgroup `H` of a topological group `G`, the right action of `H` on `G`
is proper. Note that the corresponding statement for the left action can be proven by
`inferInstance`. -/
@[to_additive /-- Given a closed subgroup `H` of an additive topological group `G`, the right
action of `H` on `G` is proper. Note that the corresponding statement for the left action can be
proven by `inferInstance`. -/]
instance [IsTopologicalGroup G] {H : Subgroup G} [H_closed : IsClosed (H : Set G)] :
    ProperSMul H.op G :=
  have : IsClosed (H.op : Set G·µê·µí·µñ) := H_closed.preimage MulOpposite.continuous_unop
  inferInstance

@[to_additive]
instance QuotientGroup.instT2Space [IsTopologicalGroup G] {H : Subgroup G} [IsClosed (H : Set G)] :
    T2Space (G ‚ß∏ H) :=
  t2Space_quotient_mulAction_of_properSMul

/-- If `G` acts on `X` properly, then the map `G √ó T ‚Üí X √ó T, (g, t) ‚Ü¶ (g ‚Ä¢ t, t)` is still
proper for *any* subset `T` of `X`. -/
@[to_additive
/-- If `G` acts on `X` properly, then the map `G √ó T ‚Üí X √ó T, (g, t) ‚Ü¶ (g +·µ• t, t)` is still
proper for *any* subset `T` of `X`. -/]
lemma ProperSMul.isProperMap_smul_pair_set [ProperSMul G X] {t : Set X} :
    IsProperMap (fun (gx : G √ó t) ‚Ü¶ ((gx.1 ‚Ä¢ gx.2, gx.2) : X √ó t)) := by
  let Œ¶ : G √ó X ‚Üí X √ó X := fun gx ‚Ü¶ (gx.1 ‚Ä¢ gx.2, gx.2)
  have Œ¶_proper : IsProperMap Œ¶ := ProperSMul.isProperMap_smul_pair
  let Œ± : G √ó t ‚âÉ‚Çú (Œ¶ ‚Åª¬π' (snd ‚Åª¬π' t)) :=
    have : univ √óÀ¢ t = Œ¶ ‚Åª¬π' (snd ‚Åª¬π' t) := by ext; simp [Œ¶]
    Homeomorph.Set.univ G |>.symm.prodCongr (.refl t) |>.trans
      ((Homeomorph.Set.prod _ t).symm) |>.trans (Homeomorph.setCongr this)
  let Œ≤ : X √ó t ‚âÉ‚Çú (snd ‚Åª¬π' t) :=
    Homeomorph.Set.univ X |>.symm.prodCongr (.refl t) |>.trans
      ((Homeomorph.Set.prod _ t).symm) |>.trans (Homeomorph.setCongr univ_prod)
  exact Œ≤.symm.isProperMap.comp (Œ¶_proper.restrictPreimage (snd ‚Åª¬π' t)) |>.comp Œ±.isProperMap

open Pointwise in
/-- If `G` acts on `X` properly, the set `s ‚Ä¢ t` is closed when `s : Set G` is *closed* and
`t : Set X` is *compact*.

See also `IsClosed.smul_left_of_isCompact` for a version with the assumptions on `s` and `t`
reversed. -/
@[to_additive
/-- If `G` acts on `X` properly, the set `s +·µ• t` is closed when `s : Set G` is *closed* and
`t : Set X` is *compact*. In particular, this applies when the action comes from an
`IsTopologicalAddTorsor`.

See also `IsClosed.vadd_left_of_isCompact` for a version with the assumptions on `s` and `t`
reversed. -/]
theorem IsClosed.smul_right_of_isCompact [ProperSMul G X] {s : Set G} {t : Set X} (hs : IsClosed s)
    (ht : IsCompact t) : IsClosed (s ‚Ä¢ t) := by
  let Œ® : G √ó t ‚Üí X √ó t := fun gx ‚Ü¶ (gx.1 ‚Ä¢ gx.2, gx.2)
  have Œ®_proper : IsProperMap Œ® := ProperSMul.isProperMap_smul_pair_set
  have : s ‚Ä¢ t = (fst ‚àò Œ®) '' (fst ‚Åª¬π' s) :=
    subset_antisymm
      (smul_subset_iff.mpr fun g hg x hx ‚Ü¶ mem_image_of_mem (fst ‚àò Œ®) (x := ‚ü®g, ‚ü®x, hx‚ü©‚ü©) hg)
      (image_subset_iff.mpr fun ‚ü®g, ‚ü®x, hx‚ü©‚ü© hg ‚Ü¶ smul_mem_smul hg hx)
  rw [this]
  have : CompactSpace t := isCompact_iff_compactSpace.mp ht
  exact (isProperMap_fst_of_compactSpace.comp Œ®_proper).isClosedMap _ (hs.preimage continuous_fst)

/-! One may expect `IsClosed.smul_right_of_isCompact` to hold for arbitrary continuous actions,
but such a lemma can't be true in this level of generality. For a counterexample, consider
`‚Ñö` acting on `‚Ñù` by translation, and let `s : Set ‚Ñö := univ`, `t : set ‚Ñù := {0}`. Then `s` is
closed and `t` is compact, but `s +·µ• t` is the set of all rationals, which is definitely not
closed in `‚Ñù`. -/
