/-
Copyright (c) 2021 Alex Kontorovich, Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alex Kontorovich, Heather Macbeth
-/
import Mathlib.Algebra.Group.Pointwise.Set.Lattice
import Mathlib.Algebra.GroupWithZero.Action.Pointwise.Set
import Mathlib.Algebra.Module.ULift
import Mathlib.GroupTheory.GroupAction.Defs
import Mathlib.Topology.Algebra.Constructions
import Mathlib.Topology.Algebra.Support

/-!
# Monoid actions continuous in the second variable

In this file we define class `ContinuousConstSMul`. We say `ContinuousConstSMul Î“ T` if
`Î“` acts on `T` and for each `Î³`, the map `x â†¦ Î³ â€¢ x` is continuous. (This differs from
`ContinuousSMul`, which requires simultaneous continuity in both variables.)

## Main definitions

* `ContinuousConstSMul Î“ T` : typeclass saying that the map `x â†¦ Î³ â€¢ x` is continuous on `T`;
* `ProperlyDiscontinuousSMul`: says that the scalar multiplication `(â€¢) : Î“ â†’ T â†’ T`
  is properly discontinuous, that is, for any pair of compact sets `K, L` in `T`, only finitely
  many `Î³:Î“` move `K` to have nontrivial intersection with `L`.
* `Homeomorph.smul`: scalar multiplication by an element of a group `Î“` acting on `T`
  is a homeomorphism of `T`.
*`Homeomorph.smulOfNeZero`: if a group with zero `Gâ‚€` (e.g., a field) acts on `X` and `c : Gâ‚€`
  is a nonzero element of `Gâ‚€`, then scalar multiplication by `c` is a homeomorphism of `X`;
* `Homeomorph.smul`: scalar multiplication by an element of a group `G` acting on `X`
  is a homeomorphism of `X`.

## Main results

* `isOpenMap_quotient_mk'_mul` : The quotient map by a group action is open.
* `t2Space_of_properlyDiscontinuousSMul_of_t2Space` : The quotient by a discontinuous group
  action of a locally compact t2 space is t2.

## Tags

Hausdorff, discrete group, properly discontinuous, quotient space

-/

assert_not_exists IsOrderedRing

open Topology Pointwise Filter Set TopologicalSpace

/-- Class `ContinuousConstSMul Î“ T` says that the scalar multiplication `(â€¢) : Î“ â†’ T â†’ T`
is continuous in the second argument. We use the same class for all kinds of multiplicative
actions, including (semi)modules and algebras.

Note that both `ContinuousConstSMul Î± Î±` and `ContinuousConstSMul Î±áµáµ’áµ– Î±` are
weaker versions of `ContinuousMul Î±`. -/
class ContinuousConstSMul (Î“ : Type*) (T : Type*) [TopologicalSpace T] [SMul Î“ T] : Prop where
  /-- The scalar multiplication `(â€¢) : Î“ â†’ T â†’ T` is continuous in the second argument. -/
  continuous_const_smul : âˆ€ Î³ : Î“, Continuous fun x : T => Î³ â€¢ x

/-- Class `ContinuousConstVAdd Î“ T` says that the additive action `(+áµ¥) : Î“ â†’ T â†’ T`
is continuous in the second argument. We use the same class for all kinds of additive actions,
including (semi)modules and algebras.

Note that both `ContinuousConstVAdd Î± Î±` and `ContinuousConstVAdd Î±áµáµ’áµ– Î±` are
weaker versions of `ContinuousVAdd Î±`. -/
class ContinuousConstVAdd (Î“ : Type*) (T : Type*) [TopologicalSpace T] [VAdd Î“ T] : Prop where
  /-- The additive action `(+áµ¥) : Î“ â†’ T â†’ T` is continuous in the second argument. -/
  continuous_const_vadd : âˆ€ Î³ : Î“, Continuous fun x : T => Î³ +áµ¥ x

attribute [to_additive] ContinuousConstSMul

export ContinuousConstSMul (continuous_const_smul)
export ContinuousConstVAdd (continuous_const_vadd)

variable {M Î± Î² : Type*}

section SMul

variable [TopologicalSpace Î±] [SMul M Î±] [ContinuousConstSMul M Î±]

@[to_additive]
instance : ContinuousConstSMul (ULift M) Î± := âŸ¨fun Î³ â†¦ continuous_const_smul (ULift.down Î³)âŸ©

@[to_additive]
theorem Filter.Tendsto.const_smul {f : Î² â†’ Î±} {l : Filter Î²} {a : Î±} (hf : Tendsto f l (ğ“ a))
    (c : M) : Tendsto (fun x => c â€¢ f x) l (ğ“ (c â€¢ a)) :=
  ((continuous_const_smul _).tendsto _).comp hf

variable [TopologicalSpace Î²] {g : Î² â†’ Î±} {b : Î²} {s : Set Î²}

@[to_additive]
nonrec theorem ContinuousWithinAt.const_smul (hg : ContinuousWithinAt g s b) (c : M) :
    ContinuousWithinAt (fun x => c â€¢ g x) s b :=
  hg.const_smul c

@[to_additive (attr := fun_prop)]
nonrec theorem ContinuousAt.const_smul (hg : ContinuousAt g b) (c : M) :
    ContinuousAt (fun x => c â€¢ g x) b :=
  hg.const_smul c

@[to_additive (attr := fun_prop)]
theorem ContinuousOn.const_smul (hg : ContinuousOn g s) (c : M) :
    ContinuousOn (fun x => c â€¢ g x) s := fun x hx => (hg x hx).const_smul c

@[to_additive (attr := continuity, fun_prop)]
theorem Continuous.const_smul (hg : Continuous g) (c : M) : Continuous fun x => c â€¢ g x :=
  (continuous_const_smul _).comp hg

/-- If a scalar is central, then its right action is continuous when its left action is. -/
@[to_additive "If an additive action is central, then its right action is continuous when its left
action is."]
instance ContinuousConstSMul.op [SMul Máµáµ’áµ– Î±] [IsCentralScalar M Î±] :
    ContinuousConstSMul Máµáµ’áµ– Î± :=
  âŸ¨MulOpposite.rec' fun c => by simpa only [op_smul_eq_smul] using continuous_const_smul câŸ©

@[to_additive]
instance MulOpposite.continuousConstSMul : ContinuousConstSMul M Î±áµáµ’áµ– :=
  âŸ¨fun c => MulOpposite.continuous_op.comp <| MulOpposite.continuous_unop.const_smul câŸ©

@[to_additive]
instance : ContinuousConstSMul M Î±áµ’áµˆ := â€¹ContinuousConstSMul M Î±â€º

@[to_additive]
instance OrderDual.continuousConstSMul' : ContinuousConstSMul Máµ’áµˆ Î± :=
  â€¹ContinuousConstSMul M Î±â€º

@[to_additive]
instance Prod.continuousConstSMul [SMul M Î²] [ContinuousConstSMul M Î²] :
    ContinuousConstSMul M (Î± Ã— Î²) :=
  âŸ¨fun _ => (continuous_fst.const_smul _).prodMk (continuous_snd.const_smul _)âŸ©

@[to_additive]
instance {Î¹ : Type*} {Î³ : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (Î³ i)] [âˆ€ i, SMul M (Î³ i)]
    [âˆ€ i, ContinuousConstSMul M (Î³ i)] : ContinuousConstSMul M (âˆ€ i, Î³ i) :=
  âŸ¨fun _ => continuous_pi fun i => (continuous_apply i).const_smul _âŸ©

@[to_additive]
theorem IsCompact.smul {Î± Î²} [SMul Î± Î²] [TopologicalSpace Î²] [ContinuousConstSMul Î± Î²] (a : Î±)
    {s : Set Î²} (hs : IsCompact s) : IsCompact (a â€¢ s) :=
  hs.image (continuous_id.const_smul a)

@[to_additive]
theorem Specializes.const_smul {x y : Î±} (h : x â¤³ y) (c : M) : (c â€¢ x) â¤³ (c â€¢ y) :=
  h.map (continuous_const_smul c)

@[to_additive]
theorem Inseparable.const_smul {x y : Î±} (h : Inseparable x y) (c : M) :
    Inseparable (c â€¢ x) (c â€¢ y) :=
  h.map (continuous_const_smul c)

@[to_additive]
theorem Topology.IsInducing.continuousConstSMul {N Î² : Type*} [SMul N Î²] [TopologicalSpace Î²]
    {g : Î² â†’ Î±} (hg : IsInducing g) (f : N â†’ M) (hf : âˆ€ {c : N} {x : Î²}, g (c â€¢ x) = f c â€¢ g x) :
    ContinuousConstSMul N Î² where
  continuous_const_smul c := by
    simpa only [Function.comp_def, hf, hg.continuous_iff] using hg.continuous.const_smul (f c)

@[deprecated (since := "2024-10-28")]
alias Inducing.continuousConstSMul := IsInducing.continuousConstSMul

end SMul

section Monoid

variable [TopologicalSpace Î±]
variable [Monoid M] [MulAction M Î±] [ContinuousConstSMul M Î±]

@[to_additive]
instance Units.continuousConstSMul : ContinuousConstSMul MË£ Î± where
  continuous_const_smul m := continuous_const_smul (m : M)

@[to_additive]
theorem smul_closure_subset (c : M) (s : Set Î±) : c â€¢ closure s âŠ† closure (c â€¢ s) :=
  ((Set.mapsTo_image _ _).closure <| continuous_const_smul c).image_subset

@[to_additive]
theorem smul_closure_orbit_subset (c : M) (x : Î±) :
    c â€¢ closure (MulAction.orbit M x) âŠ† closure (MulAction.orbit M x) :=
  (smul_closure_subset c _).trans <| closure_mono <| MulAction.smul_orbit_subset _ _

theorem isClosed_setOf_map_smul {N : Type*} [Monoid N] (Î± Î²) [MulAction M Î±] [MulAction N Î²]
    [TopologicalSpace Î²] [T2Space Î²] [ContinuousConstSMul N Î²] (Ïƒ : M â†’ N) :
    IsClosed { f : Î± â†’ Î² | âˆ€ c x, f (c â€¢ x) = Ïƒ c â€¢ f x } := by
  simp only [Set.setOf_forall]
  exact isClosed_iInter fun c => isClosed_iInter fun x =>
    isClosed_eq (continuous_apply _) ((continuous_apply _).const_smul _)

end Monoid

section Group

variable {G : Type*} [TopologicalSpace Î±] [Group G] [MulAction G Î±] [ContinuousConstSMul G Î±]

@[to_additive]
theorem tendsto_const_smul_iff {f : Î² â†’ Î±} {l : Filter Î²} {a : Î±} (c : G) :
    Tendsto (fun x => c â€¢ f x) l (ğ“ <| c â€¢ a) â†” Tendsto f l (ğ“ a) :=
  âŸ¨fun h => by simpa only [inv_smul_smul] using h.const_smul câ»Â¹, fun h => h.const_smul _âŸ©

variable [TopologicalSpace Î²] {f : Î² â†’ Î±} {b : Î²} {s : Set Î²}

@[to_additive]
theorem continuousWithinAt_const_smul_iff (c : G) :
    ContinuousWithinAt (fun x => c â€¢ f x) s b â†” ContinuousWithinAt f s b :=
  tendsto_const_smul_iff c

@[to_additive]
theorem continuousOn_const_smul_iff (c : G) :
    ContinuousOn (fun x => c â€¢ f x) s â†” ContinuousOn f s :=
  forallâ‚‚_congr fun _ _ => continuousWithinAt_const_smul_iff c

@[to_additive]
theorem continuousAt_const_smul_iff (c : G) :
    ContinuousAt (fun x => c â€¢ f x) b â†” ContinuousAt f b :=
  tendsto_const_smul_iff c

@[to_additive]
theorem continuous_const_smul_iff (c : G) : (Continuous fun x => c â€¢ f x) â†” Continuous f := by
  simp only [continuous_iff_continuousAt, continuousAt_const_smul_iff]

/-- The homeomorphism given by scalar multiplication by a given element of a group `Î“` acting on
  `T` is a homeomorphism from `T` to itself. -/
@[to_additive (attr := simps!)]
def Homeomorph.smul (Î³ : G) : Î± â‰ƒâ‚œ Î± where
  toEquiv := MulAction.toPerm Î³
  continuous_toFun := continuous_const_smul Î³
  continuous_invFun := continuous_const_smul Î³â»Â¹

/-- The homeomorphism given by affine-addition by an element of an additive group `Î“` acting on
  `T` is a homeomorphism from `T` to itself. -/
add_decl_doc Homeomorph.vadd

@[to_additive]
theorem isOpenMap_smul (c : G) : IsOpenMap fun x : Î± => c â€¢ x :=
  (Homeomorph.smul c).isOpenMap

@[to_additive]
theorem IsOpen.smul {s : Set Î±} (hs : IsOpen s) (c : G) : IsOpen (c â€¢ s) :=
  isOpenMap_smul c s hs

@[to_additive]
theorem isClosedMap_smul (c : G) : IsClosedMap fun x : Î± => c â€¢ x :=
  (Homeomorph.smul c).isClosedMap

@[to_additive]
theorem IsClosed.smul {s : Set Î±} (hs : IsClosed s) (c : G) : IsClosed (c â€¢ s) :=
  isClosedMap_smul c s hs

@[to_additive]
theorem closure_smul (c : G) (s : Set Î±) : closure (c â€¢ s) = c â€¢ closure s :=
  ((Homeomorph.smul c).image_closure s).symm

@[to_additive]
theorem Dense.smul (c : G) {s : Set Î±} (hs : Dense s) : Dense (c â€¢ s) := by
  rw [dense_iff_closure_eq] at hs âŠ¢; rw [closure_smul, hs, smul_set_univ]

@[to_additive]
theorem interior_smul (c : G) (s : Set Î±) : interior (c â€¢ s) = c â€¢ interior s :=
  ((Homeomorph.smul c).image_interior s).symm

@[to_additive]
theorem IsOpen.smul_left {s : Set G} {t : Set Î±} (ht : IsOpen t) : IsOpen (s â€¢ t) := by
  rw [â† iUnion_smul_set]
  exact isOpen_biUnion fun a _ => ht.smul _

@[to_additive]
theorem subset_interior_smul_right {s : Set G} {t : Set Î±} : s â€¢ interior t âŠ† interior (s â€¢ t) :=
  interior_maximal (Set.smul_subset_smul_left interior_subset) isOpen_interior.smul_left

@[to_additive (attr := simp)]
theorem smul_mem_nhds_smul_iff {t : Set Î±} (g : G) {a : Î±} : g â€¢ t âˆˆ ğ“ (g â€¢ a) â†” t âˆˆ ğ“ a :=
  (Homeomorph.smul g).isOpenEmbedding.image_mem_nhds

@[to_additive] alias âŸ¨_, smul_mem_nhds_smulâŸ© := smul_mem_nhds_smul_iff

@[to_additive (attr := simp)]
theorem smul_mem_nhds_self [TopologicalSpace G] [ContinuousConstSMul G G] {g : G} {s : Set G} :
    g â€¢ s âˆˆ ğ“ g â†” s âˆˆ ğ“ 1 := by
  rw [â† smul_mem_nhds_smul_iff gâ»Â¹]; simp

end Group

section GroupWithZero

variable {Gâ‚€ : Type*} [TopologicalSpace Î±] [GroupWithZero Gâ‚€] [MulAction Gâ‚€ Î±]
  [ContinuousConstSMul Gâ‚€ Î±]

theorem tendsto_const_smul_iffâ‚€ {f : Î² â†’ Î±} {l : Filter Î²} {a : Î±} {c : Gâ‚€} (hc : c â‰  0) :
    Tendsto (fun x => c â€¢ f x) l (ğ“ <| c â€¢ a) â†” Tendsto f l (ğ“ a) :=
  tendsto_const_smul_iff (Units.mk0 c hc)

variable [TopologicalSpace Î²] {f : Î² â†’ Î±} {b : Î²} {c : Gâ‚€} {s : Set Î²}

theorem continuousWithinAt_const_smul_iffâ‚€ (hc : c â‰  0) :
    ContinuousWithinAt (fun x => c â€¢ f x) s b â†” ContinuousWithinAt f s b :=
  tendsto_const_smul_iff (Units.mk0 c hc)

theorem continuousOn_const_smul_iffâ‚€ (hc : c â‰  0) :
    ContinuousOn (fun x => c â€¢ f x) s â†” ContinuousOn f s :=
  continuousOn_const_smul_iff (Units.mk0 c hc)

theorem continuousAt_const_smul_iffâ‚€ (hc : c â‰  0) :
    ContinuousAt (fun x => c â€¢ f x) b â†” ContinuousAt f b :=
  continuousAt_const_smul_iff (Units.mk0 c hc)

theorem continuous_const_smul_iffâ‚€ (hc : c â‰  0) : (Continuous fun x => c â€¢ f x) â†” Continuous f :=
  continuous_const_smul_iff (Units.mk0 c hc)

/-- Scalar multiplication by a non-zero element of a group with zero acting on `Î±` is a
homeomorphism from `Î±` onto itself. -/
@[simps! -fullyApplied apply]
protected def Homeomorph.smulOfNeZero (c : Gâ‚€) (hc : c â‰  0) : Î± â‰ƒâ‚œ Î± :=
  Homeomorph.smul (Units.mk0 c hc)

@[simp]
theorem Homeomorph.smulOfNeZero_symm_apply {c : Gâ‚€} (hc : c â‰  0) :
    â‡‘(Homeomorph.smulOfNeZero c hc).symm = (câ»Â¹ â€¢ Â· : Î± â†’ Î±) :=
  rfl

theorem isOpenMap_smulâ‚€ {c : Gâ‚€} (hc : c â‰  0) : IsOpenMap fun x : Î± => c â€¢ x :=
  (Homeomorph.smulOfNeZero c hc).isOpenMap

theorem IsOpen.smulâ‚€ {c : Gâ‚€} {s : Set Î±} (hs : IsOpen s) (hc : c â‰  0) : IsOpen (c â€¢ s) :=
  isOpenMap_smulâ‚€ hc s hs

theorem interior_smulâ‚€ {c : Gâ‚€} (hc : c â‰  0) (s : Set Î±) : interior (c â€¢ s) = c â€¢ interior s :=
  ((Homeomorph.smulOfNeZero c hc).image_interior s).symm

theorem closure_smulâ‚€' {c : Gâ‚€} (hc : c â‰  0) (s : Set Î±) :
    closure (c â€¢ s) = c â€¢ closure s :=
  ((Homeomorph.smulOfNeZero c hc).image_closure s).symm

theorem closure_smulâ‚€ {E} [Zero E] [MulActionWithZero Gâ‚€ E] [TopologicalSpace E] [T1Space E]
    [ContinuousConstSMul Gâ‚€ E] (c : Gâ‚€) (s : Set E) : closure (c â€¢ s) = c â€¢ closure s := by
  rcases eq_or_ne c 0 with (rfl | hc)
  Â· rcases eq_empty_or_nonempty s with (rfl | hs)
    Â· simp
    Â· rw [zero_smul_set hs, zero_smul_set hs.closure]
      exact closure_singleton
  Â· exact closure_smulâ‚€' hc s

/-- `smul` is a closed map in the second argument.

The lemma that `smul` is a closed map in the first argument (for a normed space over a complete
normed field) is `isClosedMap_smul_left` in `Analysis.Normed.Module.FiniteDimension`. -/
theorem isClosedMap_smul_of_ne_zero {c : Gâ‚€} (hc : c â‰  0) : IsClosedMap fun x : Î± => c â€¢ x :=
  (Homeomorph.smulOfNeZero c hc).isClosedMap

theorem IsClosed.smul_of_ne_zero {c : Gâ‚€} {s : Set Î±} (hs : IsClosed s) (hc : c â‰  0) :
    IsClosed (c â€¢ s) :=
  isClosedMap_smul_of_ne_zero hc s hs

/-- `smul` is a closed map in the second argument.

The lemma that `smul` is a closed map in the first argument (for a normed space over a complete
normed field) is `isClosedMap_smul_left` in `Analysis.Normed.Module.FiniteDimension`. -/
theorem isClosedMap_smulâ‚€ {E : Type*} [Zero E] [MulActionWithZero Gâ‚€ E] [TopologicalSpace E]
    [T1Space E] [ContinuousConstSMul Gâ‚€ E] (c : Gâ‚€) : IsClosedMap fun x : E => c â€¢ x := by
  rcases eq_or_ne c 0 with (rfl | hne)
  Â· simp only [zero_smul]
    exact isClosedMap_const
  Â· exact (Homeomorph.smulOfNeZero c hne).isClosedMap

theorem IsClosed.smulâ‚€ {E : Type*} [Zero E] [MulActionWithZero Gâ‚€ E] [TopologicalSpace E]
    [T1Space E] [ContinuousConstSMul Gâ‚€ E] (c : Gâ‚€) {s : Set E} (hs : IsClosed s) :
    IsClosed (c â€¢ s) :=
  isClosedMap_smulâ‚€ c s hs

theorem HasCompactMulSupport.comp_smul {Î² : Type*} [One Î²] {f : Î± â†’ Î²} (h : HasCompactMulSupport f)
    {c : Gâ‚€} (hc : c â‰  0) : HasCompactMulSupport fun x => f (c â€¢ x) :=
  h.comp_homeomorph (Homeomorph.smulOfNeZero c hc)

theorem HasCompactSupport.comp_smul {Î² : Type*} [Zero Î²] {f : Î± â†’ Î²} (h : HasCompactSupport f)
    {c : Gâ‚€} (hc : c â‰  0) : HasCompactSupport fun x => f (c â€¢ x) :=
  h.comp_homeomorph (Homeomorph.smulOfNeZero c hc)

attribute [to_additive existing HasCompactSupport.comp_smul] HasCompactMulSupport.comp_smul

end GroupWithZero

namespace IsUnit

variable [Monoid M] [TopologicalSpace Î±] [MulAction M Î±] [ContinuousConstSMul M Î±]

nonrec theorem tendsto_const_smul_iff {f : Î² â†’ Î±} {l : Filter Î²} {a : Î±} {c : M} (hc : IsUnit c) :
    Tendsto (fun x => c â€¢ f x) l (ğ“ <| c â€¢ a) â†” Tendsto f l (ğ“ a) :=
  tendsto_const_smul_iff hc.unit

variable [TopologicalSpace Î²] {f : Î² â†’ Î±} {b : Î²} {c : M} {s : Set Î²}

nonrec theorem continuousWithinAt_const_smul_iff (hc : IsUnit c) :
    ContinuousWithinAt (fun x => c â€¢ f x) s b â†” ContinuousWithinAt f s b :=
  continuousWithinAt_const_smul_iff hc.unit

nonrec theorem continuousOn_const_smul_iff (hc : IsUnit c) :
    ContinuousOn (fun x => c â€¢ f x) s â†” ContinuousOn f s :=
  continuousOn_const_smul_iff hc.unit

nonrec theorem continuousAt_const_smul_iff (hc : IsUnit c) :
    ContinuousAt (fun x => c â€¢ f x) b â†” ContinuousAt f b :=
  continuousAt_const_smul_iff hc.unit

nonrec theorem continuous_const_smul_iff (hc : IsUnit c) :
    (Continuous fun x => c â€¢ f x) â†” Continuous f :=
  continuous_const_smul_iff hc.unit

nonrec theorem isOpenMap_smul (hc : IsUnit c) : IsOpenMap fun x : Î± => c â€¢ x :=
  isOpenMap_smul hc.unit

nonrec theorem isClosedMap_smul (hc : IsUnit c) : IsClosedMap fun x : Î± => c â€¢ x :=
  isClosedMap_smul hc.unit

nonrec theorem smul_mem_nhds_smul_iff (hc : IsUnit c) {s : Set Î±} {a : Î±} :
    c â€¢ s âˆˆ ğ“ (c â€¢ a) â†” s âˆˆ ğ“ a :=
  smul_mem_nhds_smul_iff hc.unit

end IsUnit

-- TODO: use `Set.Nonempty`
/-- Class `ProperlyDiscontinuousSMul Î“ T` says that the scalar multiplication `(â€¢) : Î“ â†’ T â†’ T`
is properly discontinuous, that is, for any pair of compact sets `K, L` in `T`, only finitely many
`Î³:Î“` move `K` to have nontrivial intersection with `L`.
-/
class ProperlyDiscontinuousSMul (Î“ : Type*) (T : Type*) [TopologicalSpace T] [SMul Î“ T] :
    Prop where
  /-- Given two compact sets `K` and `L`, `Î³ â€¢ K âˆ© L` is nonempty for finitely many `Î³`. -/
  finite_disjoint_inter_image :
    âˆ€ {K L : Set T}, IsCompact K â†’ IsCompact L â†’ Set.Finite { Î³ : Î“ | (Î³ â€¢ Â·) '' K âˆ© L â‰  âˆ… }

/-- Class `ProperlyDiscontinuousVAdd Î“ T` says that the additive action `(+áµ¥) : Î“ â†’ T â†’ T`
is properly discontinuous, that is, for any pair of compact sets `K, L` in `T`, only finitely many
`Î³:Î“` move `K` to have nontrivial intersection with `L`.
-/
class ProperlyDiscontinuousVAdd (Î“ : Type*) (T : Type*) [TopologicalSpace T] [VAdd Î“ T] :
  Prop where
  /-- Given two compact sets `K` and `L`, `Î³ +áµ¥ K âˆ© L` is nonempty for finitely many `Î³`. -/
  finite_disjoint_inter_image :
    âˆ€ {K L : Set T}, IsCompact K â†’ IsCompact L â†’ Set.Finite { Î³ : Î“ | (Î³ +áµ¥ Â·) '' K âˆ© L â‰  âˆ… }

attribute [to_additive] ProperlyDiscontinuousSMul

export ProperlyDiscontinuousSMul (finite_disjoint_inter_image)
export ProperlyDiscontinuousVAdd (finite_disjoint_inter_image)

section

variable (Î“ T : Type*) {T} [TopologicalSpace T] [SMul Î“ T] [ProperlyDiscontinuousSMul Î“ T] (x : T)

@[to_additive] lemma ProperlyDiscontinuousSMul.finite_stabilizer' : {Î³ : Î“ | Î³ â€¢ x = x}.Finite := by
  simp_rw [â†mem_singleton_iff, â†singleton_inter_nonempty, â†image_singleton, nonempty_iff_ne_empty]
  exact finite_disjoint_inter_image isCompact_singleton isCompact_singleton

@[to_additive] lemma ProperlyDiscontinuousSMul.disjoint_image_nhds
    [T2Space T] [WeaklyLocallyCompactSpace T] [ContinuousConstSMul Î“ T] (x : T) :
    âˆƒ U âˆˆ ğ“ x, âˆ€ Î³ : Î“, (Î³ â€¢ Â·) '' U âˆ© U â‰  âˆ… â†’ Î³ â€¢ x = x := by
  obtain âŸ¨V, V_cpt, V_nhdâŸ© := exists_compact_mem_nhds x
  let Î“â‚€ := {Î³ : Î“ | (Î³ â€¢ Â·) '' V âˆ© V â‰  âˆ… âˆ§ Î³ â€¢ x â‰  x}
  have : Finite Î“â‚€ := finite_coe_iff.mpr
    ((finite_disjoint_inter_image V_cpt V_cpt).subset fun _ â†¦ And.left)
  choose u v hu hv u_v_disjoint using fun Î³ : Î“â‚€ â†¦ t2_separation_nhds Î³.2.2
  refine âŸ¨V âˆ© â‹‚ Î³ : Î“â‚€, (Î³.1 â€¢ Â·) â»Â¹' u Î³ âˆ© v Î³, inter_mem V_nhd (iInter_mem.mpr fun Î³ â†¦
    inter_mem ((continuous_const_smul _).continuousAt <| hu Î³) (hv Î³)), fun Î³ hÎ³ â†¦ ?_âŸ©
  obtain âŸ¨_, âŸ¨z, hz, rflâŸ©, hÎ³zâŸ© := nonempty_iff_ne_empty.mpr hÎ³
  by_contra h
  rw [mem_inter_iff, mem_iInter] at hz hÎ³z
  let Î³ : Î“â‚€ := âŸ¨Î³, nonempty_iff_ne_empty.mp âŸ¨_, âŸ¨z, hz.1, rflâŸ©, hÎ³z.1âŸ©, hâŸ©
  exact (u_v_disjoint Î³).le_bot âŸ¨(hz.2 Î³).1, (hÎ³z.2 Î³).2âŸ©

end

variable {Î“ : Type*} [Group Î“] {T : Type*} [TopologicalSpace T] [MulAction Î“ T]

/-- A finite group action is always properly discontinuous. -/
@[to_additive "A finite group action is always properly discontinuous."]
instance (priority := 100) Finite.to_properlyDiscontinuousSMul [Finite Î“] :
    ProperlyDiscontinuousSMul Î“ T where finite_disjoint_inter_image _ _ := Set.toFinite _

@[to_additive] lemma ProperlyDiscontinuousSMul.finite_stabilizer [ProperlyDiscontinuousSMul Î“ T]
    (x : T) : (MulAction.stabilizer Î“ x : Set Î“).Finite :=
  ProperlyDiscontinuousSMul.finite_stabilizer' Î“ x

/-- The quotient map by a group action is open, i.e. the quotient by a group action is an open
  quotient. -/
@[to_additive "The quotient map by a group action is open, i.e. the quotient by a group
action is an open quotient. "]
theorem isOpenMap_quotient_mk'_mul [ContinuousConstSMul Î“ T] :
    letI := MulAction.orbitRel Î“ T
    IsOpenMap (Quotient.mk' : T â†’ Quotient (MulAction.orbitRel Î“ T)) := fun U hU => by
  rw [isOpen_coinduced, MulAction.quotient_preimage_image_eq_union_mul U]
  exact isOpen_iUnion fun Î³ => isOpenMap_smul Î³ U hU

@[to_additive]
theorem MulAction.isOpenQuotientMap_quotientMk [ContinuousConstSMul Î“ T] :
    IsOpenQuotientMap (Quotient.mk (MulAction.orbitRel Î“ T)) :=
  âŸ¨Quot.mk_surjective, continuous_quot_mk, isOpenMap_quotient_mk'_mulâŸ©

/-- The quotient by a discontinuous group action of a locally compact t2 space is t2. -/
@[to_additive "The quotient by a discontinuous group action of a locally compact t2
space is t2."]
instance (priority := 100) t2Space_of_properlyDiscontinuousSMul_of_t2Space [T2Space T]
    [LocallyCompactSpace T] [ContinuousConstSMul Î“ T] [ProperlyDiscontinuousSMul Î“ T] :
    T2Space (Quotient (MulAction.orbitRel Î“ T)) := by
  letI := MulAction.orbitRel Î“ T
  set Q := Quotient (MulAction.orbitRel Î“ T)
  rw [t2Space_iff_nhds]
  let f : T â†’ Q := Quotient.mk'
  have f_op : IsOpenMap f := isOpenMap_quotient_mk'_mul
  rintro âŸ¨xâ‚€âŸ© âŸ¨yâ‚€âŸ© (hxy : f xâ‚€ â‰  f yâ‚€)
  show âˆƒ U âˆˆ ğ“ (f xâ‚€), âˆƒ V âˆˆ ğ“ (f yâ‚€), _
  have hÎ³xâ‚€yâ‚€ : âˆ€ Î³ : Î“, Î³ â€¢ xâ‚€ â‰  yâ‚€ := not_exists.mp (mt Quotient.sound hxy.symm :)
  obtain âŸ¨Kâ‚€, hKâ‚€, Kâ‚€_inâŸ© := exists_compact_mem_nhds xâ‚€
  obtain âŸ¨Lâ‚€, hLâ‚€, Lâ‚€_inâŸ© := exists_compact_mem_nhds yâ‚€
  let bad_Î“_set := { Î³ : Î“ | (Î³ â€¢ Â·) '' Kâ‚€ âˆ© Lâ‚€ â‰  âˆ… }
  have bad_Î“_finite : bad_Î“_set.Finite := finite_disjoint_inter_image (Î“ := Î“) hKâ‚€ hLâ‚€
  choose u v hu hv u_v_disjoint using fun Î³ => t2_separation_nhds (hÎ³xâ‚€yâ‚€ Î³)
  let Uâ‚€â‚€ := â‹‚ Î³ âˆˆ bad_Î“_set, (Î³ â€¢ Â·) â»Â¹' u Î³
  let Uâ‚€ := Uâ‚€â‚€ âˆ© Kâ‚€
  let Vâ‚€â‚€ := â‹‚ Î³ âˆˆ bad_Î“_set, v Î³
  let Vâ‚€ := Vâ‚€â‚€ âˆ© Lâ‚€
  have U_nhds : f '' Uâ‚€ âˆˆ ğ“ (f xâ‚€) := by
    refine f_op.image_mem_nhds (inter_mem ((biInter_mem bad_Î“_finite).mpr fun Î³ _ => ?_) Kâ‚€_in)
    exact (continuous_const_smul _).continuousAt (hu Î³)
  have V_nhds : f '' Vâ‚€ âˆˆ ğ“ (f yâ‚€) :=
    f_op.image_mem_nhds (inter_mem ((biInter_mem bad_Î“_finite).mpr fun Î³ _ => hv Î³) Lâ‚€_in)
  refine âŸ¨f '' Uâ‚€, U_nhds, f '' Vâ‚€, V_nhds, MulAction.disjoint_image_image_iff.2 ?_âŸ©
  rintro x âŸ¨x_in_Uâ‚€â‚€, x_in_Kâ‚€âŸ© Î³
  by_cases H : Î³ âˆˆ bad_Î“_set
  Â· exact fun h => (u_v_disjoint Î³).le_bot âŸ¨mem_iInterâ‚‚.mp x_in_Uâ‚€â‚€ Î³ H, mem_iInterâ‚‚.mp h.1 Î³ HâŸ©
  Â· rintro âŸ¨-, h'âŸ©
    simp only [bad_Î“_set, image_smul, Classical.not_not, mem_setOf_eq, Ne] at H
    exact eq_empty_iff_forall_not_mem.mp H (Î³ â€¢ x) âŸ¨mem_image_of_mem _ x_in_Kâ‚€, h'âŸ©

/-- The quotient of a second countable space by a group action is second countable. -/
@[to_additive "The quotient of a second countable space by an additive group action is second
countable."]
theorem ContinuousConstSMul.secondCountableTopology [SecondCountableTopology T]
    [ContinuousConstSMul Î“ T] : SecondCountableTopology (Quotient (MulAction.orbitRel Î“ T)) :=
  TopologicalSpace.Quotient.secondCountableTopology isOpenMap_quotient_mk'_mul

section nhds

section MulAction

variable {Gâ‚€ : Type*} [GroupWithZero Gâ‚€] [MulAction Gâ‚€ Î±] [TopologicalSpace Î±]
  [ContinuousConstSMul Gâ‚€ Î±]

/-- Scalar multiplication by a nonzero scalar preserves neighborhoods. -/
theorem smul_mem_nhds_smul_iffâ‚€ {c : Gâ‚€} {s : Set Î±} {x : Î±} (hc : c â‰  0) :
    c â€¢ s âˆˆ ğ“ (c â€¢ x : Î±) â†” s âˆˆ ğ“ x :=
  smul_mem_nhds_smul_iff (Units.mk0 c hc)

alias âŸ¨_, smul_mem_nhds_smulâ‚€âŸ© := smul_mem_nhds_smul_iffâ‚€

end MulAction

section DistribMulAction

variable {Gâ‚€ : Type*} [GroupWithZero Gâ‚€] [AddMonoid Î±] [DistribMulAction Gâ‚€ Î±] [TopologicalSpace Î±]
  [ContinuousConstSMul Gâ‚€ Î±]

theorem set_smul_mem_nhds_zero_iff {s : Set Î±} {c : Gâ‚€} (hc : c â‰  0) :
    c â€¢ s âˆˆ ğ“ (0 : Î±) â†” s âˆˆ ğ“ (0 : Î±) := by
  refine Iff.trans ?_ (smul_mem_nhds_smul_iffâ‚€ hc)
  rw [smul_zero]

end DistribMulAction

end nhds
