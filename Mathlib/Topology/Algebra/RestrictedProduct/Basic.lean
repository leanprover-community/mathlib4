/-
Copyright (c) 2025 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import Mathlib.Algebra.Ring.Pi
import Mathlib.Algebra.Ring.Subring.Defs
import Mathlib.GroupTheory.GroupAction.SubMulAction
import Mathlib.Order.Filter.Cofinite -- for `Î Ê³ i, [R i, A i]` notation, confuses shake
import Mathlib.Algebra.Module.Pi

/-!
# Restricted products of sets, groups and rings

We define the **restricted product** of `R : Î¹ â†’ Type*` of types, relative to
a family of subsets `A : (i : Î¹) â†’ Set (R i)` and a filter `ğ“• : Filter Î¹`. This
is the set of all `x : Î  i, R i` such that the set `{j | x j âˆˆ A j}` belongs to `ğ“•`.
We denote it by `Î Ê³ i, [R i, A i]_[ğ“•]`.

The main case of interest, which we shall refer to as the "classical restricted product",
is that of `ğ“• = cofinite`. Recall that this is the filter of all subsets of `Î¹`, which are
*cofinite* in the sense that they have finite complement.
Hence, the associated restricted product is the set of all `x : Î  i, R i` such that
`x j âˆˆ A j` for all but finitely many `j`s. We denote it simply by `Î Ê³ i, [R i, A i]`.

Another notable case is that of the principal filter `ğ“• = ğ“Ÿ s` corresponding to some subset `s`
of `Î¹`. The associated restricted product `Î Ê³ i, [R i, A i]_[ğ“Ÿ s]` is the set of all
`x : Î  i, R i` such that `x j âˆˆ A j` for all `j âˆˆ s`. Put another way, this is just
`(Î  i âˆˆ s, A i) Ã— (Î  i âˆ‰ s, R i)`, modulo the obvious isomorphism.

We endow these types with the obvious algebraic structures. We also show various compatibility
results.

See also the file `Mathlib/Topology/Algebra/RestrictedProduct/TopologicalSpace.lean`, which
puts the structure of a topological space on a restricted product of topological spaces.

## Main definitions

* `RestrictedProduct`: the restricted product of a family `R` of types, relative to a family `A` of
  subsets and a filter `ğ“•` on the indexing set. This is denoted `Î Ê³ i, [R i, A i]_[ğ“•]`,
  or simply `Î Ê³ i, [R i, A i]` when `ğ“• = cofinite`.
* `RestrictedProduct.instDFunLike`: interpret an element of `Î Ê³ i, [R i, A i]_[ğ“•]` as an element
  of `Î  i, R i` using the `DFunLike` machinery.
* `RestrictedProduct.structureMap`: the inclusion map from `Î  i, A i` to `Î Ê³ i, [R i, A i]_[ğ“•]`.

## Notation

* `Î Ê³ i, [R i, A i]_[ğ“•]` is `RestrictedProduct R A ğ“•`.
* `Î Ê³ i, [R i, A i]` is `RestrictedProduct R A cofinite`.

## Tags

restricted product, adeles, ideles
-/

open Set Filter

variable {Î¹ : Type*}
variable (R : Î¹ â†’ Type*) (A : (i : Î¹) â†’ Set (R i))

/-!
## Definition and elementary maps
-/

/-- The **restricted product** of a family `R : Î¹ â†’ Type*` of types, relative to subsets
`A : (i : Î¹) â†’ Set (R i)` and the filter `ğ“• : Filter Î¹`, is the set of all `x : Î  i, R i`
such that the set `{j | x j âˆˆ A j}` belongs to `ğ“•`. We denote it by `Î Ê³ i, [R i, A i]_[ğ“•]`.

The most common use case is with `ğ“• = cofinite`, in which case the restricted product is the set
of all `x : Î  i, R i` such that `x j âˆˆ A j` for all but finitely many `j`. We denote it simply
by `Î Ê³ i, [R i, A i]`.

Similarly, if `S` is a principal filter, the restricted product `Î Ê³ i, [R i, A i]_[ğ“Ÿ s]`
is the set of all `x : Î  i, R i` such that `âˆ€ j âˆˆ S, x j âˆˆ A j`. -/
def RestrictedProduct (ğ“• : Filter Î¹) : Type _ := {x : Î  i, R i // âˆ€á¶  i in ğ“•, x i âˆˆ A i}

open Batteries.ExtendedBinder

/-- `Î Ê³ i, [R i, A i]_[ğ“•]` is `RestrictedProduct R A ğ“•`. -/
scoped[RestrictedProduct]
notation3 "Î Ê³ "(...)", ""["r:(scoped R => R)", "a:(scoped A => A)"]_[" f "]" =>
  RestrictedProduct r a f

/-- `Î Ê³ i, [R i, A i]` is `RestrictedProduct R A cofinite`. -/
scoped[RestrictedProduct]
notation3"Î Ê³ "(...)", ""["r:(scoped R => R)", "a:(scoped A => A)"]" =>
  RestrictedProduct r a cofinite

namespace RestrictedProduct

open scoped RestrictedProduct

variable {ğ“• ğ“– : Filter Î¹}

instance : DFunLike (Î Ê³ i, [R i, A i]_[ğ“•]) Î¹ R where
  coe x i := x.1 i
  coe_injective' _ _ := Subtype.ext

variable {R A} in
/-- Constructor for `RestrictedProduct`. -/
abbrev mk (x : Î  i, R i) (hx : âˆ€á¶  i in ğ“•, x i âˆˆ A i) : Î Ê³ i, [R i, A i]_[ğ“•] :=
  âŸ¨x, hxâŸ©

@[simp]
lemma mk_apply (x : Î  i, R i) (hx : âˆ€á¶  i in ğ“•, x i âˆˆ A i) (i : Î¹) :
    (mk x hx) i = x i := rfl

@[ext]
lemma ext {x y : Î Ê³ i, [R i, A i]_[ğ“•]} (h : âˆ€ i, x i = y i) : x = y :=
  Subtype.ext <| funext h

lemma range_coe :
    range ((â†‘) : Î Ê³ i, [R i, A i]_[ğ“•] â†’ Î  i, R i) = {x | âˆ€á¶  i in ğ“•, x i âˆˆ A i} :=
  Subtype.range_val_subtype

lemma range_coe_principal {S : Set Î¹} :
    range ((â†‘) : Î Ê³ i, [R i, A i]_[ğ“Ÿ S] â†’ Î  i, R i) = S.pi A :=
  range_coe R A

@[simp] lemma eventually (x : Î Ê³ i, [R i, A i]_[ğ“•]) : âˆ€á¶  i in ğ“•, x i âˆˆ A i := x.2

variable (ğ“•) in
/-- The *structure map* of the restricted product is the obvious inclusion from `Î  i, A i`
into `Î Ê³ i, [R i, A i]_[ğ“•]`. -/
def structureMap (x : Î  i, A i) : Î Ê³ i, [R i, A i]_[ğ“•] :=
  âŸ¨fun i â†¦ x i, .of_forall fun i â†¦ (x i).2âŸ©

/-- If `ğ“• â‰¤ ğ“–`, the restricted product `Î Ê³ i, [R i, A i]_[ğ“–]` is naturally included in
`Î Ê³ i, [R i, A i]_[ğ“•]`. This is the corresponding map. -/
def inclusion (h : ğ“• â‰¤ ğ“–) (x : Î Ê³ i, [R i, A i]_[ğ“–]) :
    Î Ê³ i, [R i, A i]_[ğ“•] :=
  âŸ¨x, x.2.filter_mono hâŸ©

variable (ğ“•) in
lemma inclusion_eq_id : inclusion R A (le_refl ğ“•) = id := rfl

lemma exists_inclusion_eq_of_eventually (h : ğ“• â‰¤ ğ“–) {x : Î Ê³ i, [R i, A i]_[ğ“•]}
    (hxğ“– : âˆ€á¶  i in ğ“–, x i âˆˆ A i) :
    âˆƒ x' : Î Ê³ i, [R i, A i]_[ğ“–], inclusion R A h x' = x :=
  âŸ¨âŸ¨x.1, hxğ“–âŸ©, rflâŸ©

lemma exists_structureMap_eq_of_forall {x : Î Ê³ i, [R i, A i]_[ğ“•]}
    (hx : âˆ€ i, x.1 i âˆˆ A i) :
    âˆƒ x' : Î  i, A i, structureMap R A ğ“• x' = x :=
  âŸ¨fun i â†¦ âŸ¨x i, hx iâŸ©, rflâŸ©

lemma range_inclusion (h : ğ“• â‰¤ ğ“–) :
    Set.range (inclusion R A h) = {x | âˆ€á¶  i in ğ“–, x i âˆˆ A i} :=
  subset_antisymm (range_subset_iff.mpr fun x â†¦ x.2)
    (fun _ hx â†¦ mem_range.mpr <| exists_inclusion_eq_of_eventually R A h hx)

lemma range_structureMap :
    Set.range (structureMap R A ğ“•) = {f | âˆ€ i, f.1 i âˆˆ A i} :=
  subset_antisymm (range_subset_iff.mpr fun x i â†¦ (x i).2)
    (fun _ hx â†¦ mem_range.mpr <| exists_structureMap_eq_of_forall R A hx)

section Algebra
/-!
## Algebraic instances on restricted products

In this section, we endow the restricted product with its algebraic instances.
To avoid any unnecessary coercions, we use subobject classes for the subset `B i` of each `R i`.
-/

variable {S : Î¹ â†’ Type*} -- subobject type
variable [Î  i, SetLike (S i) (R i)]
variable {B : Î  i, S i}

@[to_additive]
instance [Î  i, One (R i)] [âˆ€ i, OneMemClass (S i) (R i)] : One (Î Ê³ i, [R i, B i]_[ğ“•]) where
  one := âŸ¨fun _ â†¦ 1, .of_forall fun _ â†¦ one_mem _âŸ©

@[to_additive (attr := simp)]
lemma one_apply [Î  i, One (R i)] [âˆ€ i, OneMemClass (S i) (R i)] (i : Î¹) :
    (1 : Î Ê³ i, [R i, B i]_[ğ“•]) i = 1 :=
  rfl

@[to_additive]
instance [Î  i, Inv (R i)] [âˆ€ i, InvMemClass (S i) (R i)] : Inv (Î Ê³ i, [R i, B i]_[ğ“•]) where
  inv x := âŸ¨fun i â†¦ (x i)â»Â¹, x.2.mono fun _ â†¦ inv_memâŸ©

@[to_additive (attr := simp)]
lemma inv_apply [Î  i, Inv (R i)] [âˆ€ i, InvMemClass (S i) (R i)]
    (x : Î Ê³ i, [R i, B i]_[ğ“•]) (i : Î¹) : (xâ»Â¹) i = (x i)â»Â¹ :=
  rfl

@[to_additive]
instance [Î  i, Mul (R i)] [âˆ€ i, MulMemClass (S i) (R i)] : Mul (Î Ê³ i, [R i, B i]_[ğ“•]) where
  mul x y := âŸ¨fun i â†¦ x i * y i, y.2.mp (x.2.mono fun _ â†¦ mul_mem)âŸ©

@[to_additive (attr := simp)]
lemma mul_apply [Î  i, Mul (R i)] [âˆ€ i, MulMemClass (S i) (R i)]
    (x y : Î Ê³ i, [R i, B i]_[ğ“•]) (i : Î¹) : (x * y) i = x i * y i :=
  rfl

@[to_additive]
instance {G : Type*} [Î  i, SMul G (R i)] [âˆ€ i, SMulMemClass (S i) G (R i)] :
    SMul G (Î Ê³ i, [R i, B i]_[ğ“•]) where
  smul g x := âŸ¨fun i â†¦ g â€¢ (x i), x.2.mono fun _ â†¦ SMulMemClass.smul_mem gâŸ©

@[to_additive (attr := simp)]
lemma smul_apply {G : Type*} [Î  i, SMul G (R i)] [âˆ€ i, SMulMemClass (S i) G (R i)] (g : G)
    (x : Î Ê³ i, [R i, B i]_[ğ“•]) (i : Î¹) : (g â€¢ x) i = g â€¢ x i :=
  rfl

@[to_additive]
instance [Î  i, DivInvMonoid (R i)] [âˆ€ i, SubgroupClass (S i) (R i)] :
    Div (Î Ê³ i, [R i, B i]_[ğ“•]) where
  div x y := âŸ¨fun i â†¦ x i / y i, y.2.mp (x.2.mono fun _ â†¦ div_mem)âŸ©

@[to_additive (attr := simp)]
lemma div_apply [Î  i, DivInvMonoid (R i)] [âˆ€ i, SubgroupClass (S i) (R i)]
    (x y : Î Ê³ i, [R i, B i]_[ğ“•]) (i : Î¹) : (x / y) i = x i / y i :=
  rfl

instance instNSMul [Î  i, AddMonoid (R i)] [âˆ€ i, AddSubmonoidClass (S i) (R i)] :
    SMul â„• (Î Ê³ i, [R i, B i]_[ğ“•]) where
  smul n x := âŸ¨fun i â†¦ n â€¢ (x i), x.2.mono fun _ hi â†¦ nsmul_mem hi nâŸ©

@[to_additive existing instNSMul]
instance [Î  i, Monoid (R i)] [âˆ€ i, SubmonoidClass (S i) (R i)] :
    Pow (Î Ê³ i, [R i, B i]_[ğ“•]) â„• where
  pow x n := âŸ¨fun i â†¦ x i ^ n, x.2.mono fun _ hi â†¦ pow_mem hi nâŸ©

@[to_additive]
lemma pow_apply [Î  i, Monoid (R i)] [âˆ€ i, SubmonoidClass (S i) (R i)]
    (x : Î Ê³ i, [R i, B i]_[ğ“•]) (n : â„•) (i : Î¹) : (x ^ n) i = x i ^ n :=
  rfl

@[to_additive]
instance [Î  i, Monoid (R i)] [âˆ€ i, SubmonoidClass (S i) (R i)] :
    Monoid (Î Ê³ i, [R i, B i]_[ğ“•]) :=
  DFunLike.coe_injective.monoid _ rfl (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl)

@[to_additive]
instance [Î  i, CommMonoid (R i)] [âˆ€ i, SubmonoidClass (S i) (R i)] :
    CommMonoid (Î Ê³ i, [R i, B i]_[ğ“•]) :=
  DFunLike.coe_injective.commMonoid _ rfl (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl)

instance instZSMul [Î  i, SubNegMonoid (R i)] [âˆ€ i, AddSubgroupClass (S i) (R i)] :
    SMul â„¤ (Î Ê³ i, [R i, B i]_[ğ“•]) where
  smul n x := âŸ¨fun i â†¦ n â€¢ x i, x.2.mono fun _ hi â†¦ zsmul_mem hi nâŸ©

@[to_additive existing instZSMul]
instance [Î  i, DivInvMonoid (R i)] [âˆ€ i, SubgroupClass (S i) (R i)] :
    Pow (Î Ê³ i, [R i, B i]_[ğ“•]) â„¤ where
  pow x n := âŸ¨fun i â†¦ x i ^ n, x.2.mono fun _ hi â†¦ zpow_mem hi nâŸ©

@[to_additive]
lemma zpow_apply [Î  i, DivInvMonoid (R i)] [âˆ€ i, SubgroupClass (S i) (R i)]
    (x : Î Ê³ i, [R i, B i]_[ğ“•]) (n : â„¤) (i : Î¹) : (x ^ n) i = x i ^ n :=
  rfl

instance [Î  i, AddMonoidWithOne (R i)] [âˆ€ i, AddSubmonoidWithOneClass (S i) (R i)] :
    NatCast (Î Ê³ i, [R i, B i]_[ğ“•]) where
  natCast n := âŸ¨fun _ â†¦ n, .of_forall fun _ â†¦ natCast_mem _ nâŸ©

@[to_additive]
instance [Î  i, Group (R i)] [âˆ€ i, SubgroupClass (S i) (R i)] :
    Group (Î Ê³ i, [R i, B i]_[ğ“•]) :=
  DFunLike.coe_injective.group _ rfl (fun _ _ â†¦ rfl) (fun _ â†¦ rfl) (fun _ _ â†¦ rfl)
    (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl)

@[to_additive]
instance [Î  i, CommGroup (R i)] [âˆ€ i, SubgroupClass (S i) (R i)] :
    CommGroup (Î Ê³ i, [R i, B i]_[ğ“•]) :=
  DFunLike.coe_injective.commGroup _ rfl (fun _ _ â†¦ rfl) (fun _ â†¦ rfl) (fun _ _ â†¦ rfl)
    (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl)

instance [Î  i, Ring (R i)] [âˆ€ i, SubringClass (S i) (R i)] :
    IntCast (Î Ê³ i, [R i, B i]_[ğ“•]) where
  intCast n := âŸ¨fun _ â†¦ n, .of_forall fun _ â†¦ intCast_mem _ nâŸ©

instance [Î  i, Ring (R i)] [âˆ€ i, SubringClass (S i) (R i)] :
    Ring (Î Ê³ i, [R i, B i]_[ğ“•]) :=
  DFunLike.coe_injective.ring _ rfl rfl (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl) (fun _ â†¦ rfl)
    (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl) (fun _ â†¦ rfl) (fun _ â†¦ rfl)

instance [Î  i, CommRing (R i)] [âˆ€ i, SubringClass (S i) (R i)] :
    CommRing (Î Ê³ i, [R i, B i]_[ğ“•]) where
  mul_comm _ _ := DFunLike.coe_injective <| funext (fun _ â†¦ mul_comm _ _)

variable {R} in
/-- The coercion from the restricted product of monoids `A i` to the (normal) product
is a monoid homomorphism. -/
@[to_additive /-- The coercion from the restricted product of additive monoids `A i` to the
(normal) product is an additive monoid homomorphism. -/]
def coeMonoidHom [âˆ€ i, Monoid (R i)] [âˆ€ i, SubmonoidClass (S i) (R i)] :
    Î Ê³ i, [R i, B i]_[ğ“•] â†’* Î  i, R i where
  toFun := (â†‘)
  map_one' := rfl
  map_mul' _ _ := rfl

instance {Râ‚€ : Type*} [Semiring Râ‚€] [Î  i, AddCommMonoid (R i)] [Î  i, Module Râ‚€ (R i)]
    [âˆ€ i, AddSubmonoidClass (S i) (R i)] [âˆ€ i, SMulMemClass (S i) Râ‚€ (R i)] :
  Module Râ‚€ (Î Ê³ i, [R i, B i]_[ğ“•]) :=
  DFunLike.coe_injective.module Râ‚€ (M := Î  i, R i) coeAddMonoidHom (fun _ _ â†¦ rfl)

end Algebra

section eval

variable {S : Î¹ â†’ Type*}
variable [Î  i, SetLike (S i) (R i)]
variable {B : Î  i, S i}

/-- `RestrictedProduct.evalMonoidHom j` is the monoid homomorphism from the restricted
product `Î Ê³ i, [R i, B i]_[ğ“•]` to the component `R j`.
-/
@[to_additive /-- `RestrictedProduct.evalAddMonoidHom j` is the monoid homomorphism from the
restricted product `Î Ê³ i, [R i, B i]_[ğ“•]` to the component `R j`. -/]
def evalMonoidHom (j : Î¹) [Î  i, Monoid (R i)] [âˆ€ i, SubmonoidClass (S i) (R i)] :
    (Î Ê³ i, [R i, B i]_[ğ“•]) â†’* R j where
  toFun x := x j
  map_one' := rfl
  map_mul' _ _ := rfl

@[simp]
lemma evalMonoidHom_apply [Î  i, Monoid (R i)] [âˆ€ i, SubmonoidClass (S i) (R i)]
    (x : Î Ê³ i, [R i, B i]_[ğ“•]) (j : Î¹) : evalMonoidHom R j x = x j :=
  rfl

/-- `RestrictedProduct.evalRingHom j` is the ring homomorphism from the restricted
product `Î Ê³ i, [R i, B i]_[ğ“•]` to the component `R j`.
-/
def evalRingHom (j : Î¹) [Î  i, Ring (R i)] [âˆ€ i, SubringClass (S i) (R i)] :
    (Î Ê³ i, [R i, B i]_[ğ“•]) â†’+* R j where
  __ := evalMonoidHom R j
  __ := evalAddMonoidHom R j

@[simp]
lemma evalRingHom_apply [Î  i, Ring (R i)] [âˆ€ i, SubringClass (S i) (R i)]
    (x : Î Ê³ i, [R i, B i]_[ğ“•]) (j : Î¹) : evalRingHom R j x = x j :=
  rfl

end eval

section map

variable {Î¹â‚ Î¹â‚‚ : Type*}
variable (Râ‚ : Î¹â‚ â†’ Type*) (Râ‚‚ : Î¹â‚‚ â†’ Type*)
variable {ğ“•â‚ : Filter Î¹â‚} {ğ“•â‚‚ : Filter Î¹â‚‚}
variable {Aâ‚ : (i : Î¹â‚) â†’ Set (Râ‚ i)} {Aâ‚‚ : (i : Î¹â‚‚) â†’ Set (Râ‚‚ i)}
variable {Sâ‚ : Î¹â‚ â†’ Type*} {Sâ‚‚ : Î¹â‚‚ â†’ Type*}
variable [Î  i, SetLike (Sâ‚ i) (Râ‚ i)] [Î  j, SetLike (Sâ‚‚ j) (Râ‚‚ j)]
variable {Bâ‚ : Î  i, Sâ‚ i} {Bâ‚‚ : Î  j, Sâ‚‚ j}
variable (f : Î¹â‚‚ â†’ Î¹â‚) (hf : Tendsto f ğ“•â‚‚ ğ“•â‚)

section set

variable (Ï† : âˆ€ j, Râ‚ (f j) â†’ Râ‚‚ j) (hÏ† : âˆ€á¶  j in ğ“•â‚‚, MapsTo (Ï† j) (Aâ‚ (f j)) (Aâ‚‚ j))

/--
Given two restricted products `Î Ê³ (i : Î¹â‚), [Râ‚ i, Aâ‚ i]_[ğ“•â‚]` and `Î Ê³ (j : Î¹â‚‚), [Râ‚‚ j, Aâ‚‚ j]_[ğ“•â‚‚]`,
`RestrictedProduct.mapAlong` gives a function between them. The data needed is a
function `f : Î¹â‚‚ â†’ Î¹â‚` such that `ğ“•â‚‚` tends to `ğ“•â‚` along `f`, and functions `Ï† j : Râ‚ (f j) â†’ Râ‚‚ j`
sending `Aâ‚ (f j)` into `Aâ‚‚ j` for an `ğ“•â‚‚`-large set of `j`'s.

See also `mapAlongMonoidHom`, `mapAlongAddMonoidHom` and `mapAlongRingHom` for variants.
-/
def mapAlong (x : Î Ê³ i, [Râ‚ i, Aâ‚ i]_[ğ“•â‚]) : Î Ê³ j, [Râ‚‚ j, Aâ‚‚ j]_[ğ“•â‚‚] :=
  âŸ¨fun j â†¦ Ï† j (x (f j)), by
  filter_upwards [hf.eventually x.2, hÏ†] using fun _ h1 h2 â†¦ h2 h1âŸ©

@[simp]
lemma mapAlong_apply (x : Î Ê³ i, [Râ‚ i, Aâ‚ i]_[ğ“•â‚]) (j : Î¹â‚‚) :
    x.mapAlong Râ‚ Râ‚‚ f hf Ï† hÏ† j = Ï† j (x (f j)) :=
  rfl

-- variant of `mapAlong` where the index set is constant

/-- The maps between restricted products over a fixed index type,
given maps on the factors. -/
def map {G H : Î¹ â†’ Type*}
    {C : (i : Î¹) â†’ Set (G i)}
    {D : (i : Î¹) â†’ Set (H i)} (Ï† : (i : Î¹) â†’ G i â†’ H i)
    (hÏ† : âˆ€á¶  i in ğ“•, MapsTo (Ï† i) (C i) (D i))
    (x : Î Ê³ i, [G i, C i]_[ğ“•]) : (Î Ê³ i, [H i, D i]_[ğ“•]) :=
  mapAlong G H id Filter.tendsto_id Ï† hÏ† x

@[simp]
lemma map_apply {G H : Î¹ â†’ Type*} {C : (i : Î¹) â†’ Set (G i)}
    {D : (i : Î¹) â†’ Set (H i)} (Ï† : (i : Î¹) â†’ G i â†’ H i)
    (hÏ† : âˆ€á¶  i in ğ“•, MapsTo (Ï† i) (C i) (D i))
    (x : Î Ê³ i, [G i, C i]_[ğ“•]) (j : Î¹) :
    x.map Ï† hÏ† j = Ï† j (x j) :=
  rfl

end set

section monoid

variable [Î  i, Monoid (Râ‚ i)] [Î  i, Monoid (Râ‚‚ i)] [âˆ€ i, SubmonoidClass (Sâ‚ i) (Râ‚ i)]
    [âˆ€ i, SubmonoidClass (Sâ‚‚ i) (Râ‚‚ i)] (Ï† : âˆ€ j, Râ‚ (f j) â†’* Râ‚‚ j)
    (hÏ† : âˆ€á¶  j in ğ“•â‚‚, MapsTo (Ï† j) (Bâ‚ (f j)) (Bâ‚‚ j))

/--
Given two restricted products `Î Ê³ (i : Î¹â‚), [Râ‚ i, Bâ‚ i]_[ğ“•â‚]` and `Î Ê³ (j : Î¹â‚‚), [Râ‚‚ j, Bâ‚‚ j]_[ğ“•â‚‚]`
of monoids, `RestrictedProduct.mapAlongMonoidHom` gives a monoid homomorphism between them.
The data needed is a function `f : Î¹â‚‚ â†’ Î¹â‚` such that `ğ“•â‚‚` tends to `ğ“•â‚` along `f`, and monoid
homomorphisms `Ï† j : Râ‚ (f j) â†’ Râ‚‚ j` sending `Bâ‚ (f j)` into `Bâ‚‚ j` for an `ğ“•â‚‚`-large set of `j`'s.
-/
@[to_additive
/-- Given two restricted products `Î Ê³ (i : Î¹â‚), [Râ‚ i, Bâ‚ i]_[ğ“•â‚]` and
`Î Ê³ (j : Î¹â‚‚), [Râ‚‚ j, Bâ‚‚ j]_[ğ“•â‚‚]` of additive monoids, `RestrictedProduct.mapAlongAddMonoidHom`
gives a additive monoid homomorphism between them. The data needed is a function `f : Î¹â‚‚ â†’ Î¹â‚` such
that `ğ“•â‚‚` tends to `ğ“•â‚` along `f`, and additive monoid homomorphisms `Ï† j : Râ‚ (f j) â†’ Râ‚‚ j`
sending `Bâ‚ (f j)` into `Bâ‚‚ j` for an `ğ“•â‚‚`-large set of `j`'s. -/]
def mapAlongMonoidHom : Î Ê³ i, [Râ‚ i, Bâ‚ i]_[ğ“•â‚] â†’* Î Ê³ j, [Râ‚‚ j, Bâ‚‚ j]_[ğ“•â‚‚] where
  toFun := mapAlong Râ‚ Râ‚‚ f hf (fun j r â†¦ Ï† j r) hÏ†
  map_one' := by
    ext i
    exact map_one (Ï† i)
  map_mul' x y := by
    ext i
    exact map_mul (Ï† i) _ _

@[to_additive (attr := simp)]
lemma mapAlongMonoidHom_apply (x : Î Ê³ i, [Râ‚ i, Bâ‚ i]_[ğ“•â‚]) (j : Î¹â‚‚) :
    x.mapAlongMonoidHom Râ‚ Râ‚‚ f hf Ï† hÏ† j = Ï† j (x (f j)) :=
  rfl

end monoid

section ring

variable [Î  i, Ring (Râ‚ i)] [Î  i, Ring (Râ‚‚ i)] [âˆ€ i, SubringClass (Sâ‚ i) (Râ‚ i)]
    [âˆ€ i, SubringClass (Sâ‚‚ i) (Râ‚‚ i)] (Ï† : âˆ€ j, Râ‚ (f j) â†’+* Râ‚‚ j)
    (hÏ† : âˆ€á¶  j in ğ“•â‚‚, MapsTo (Ï† j) (Bâ‚ (f j)) (Bâ‚‚ j))

/--
Given two restricted products of rings `Î Ê³ (i : Î¹â‚), [Râ‚ i, Bâ‚ i]_[ğ“•â‚]` and
`Î Ê³ (j : Î¹â‚‚), [Râ‚‚ j, Bâ‚‚ j]_[ğ“•â‚‚]`, `RestrictedProduct.mapAlongRingHom` gives a
ring homomorphism between them. The data needed is a
function `f : Î¹â‚‚ â†’ Î¹â‚` such that `ğ“•â‚‚` tends to `ğ“•â‚` along `f`, and ring homomorphisms
`Ï† j : Râ‚ (f j) â†’ Râ‚‚ j` sending `Bâ‚ (f j)` into `Bâ‚‚ j` for an `ğ“•â‚‚`-large set of `j`'s.
-/
def mapAlongRingHom : Î Ê³ i, [Râ‚ i, Bâ‚ i]_[ğ“•â‚] â†’+* Î Ê³ j, [Râ‚‚ j, Bâ‚‚ j]_[ğ“•â‚‚] where
  __ := mapAlongMonoidHom Râ‚ Râ‚‚ f hf (fun j â†¦ Ï† j) hÏ†
  __ := mapAlongAddMonoidHom Râ‚ Râ‚‚ f hf (fun j â†¦ Ï† j) hÏ†

@[simp]
lemma mapAlongRingHom_apply (x : Î Ê³ i, [Râ‚ i, Bâ‚ i]_[ğ“•â‚]) (j : Î¹â‚‚) :
    x.mapAlongRingHom Râ‚ Râ‚‚ f hf Ï† hÏ† j = Ï† j (x (f j)) :=
  rfl

end ring

end map

end RestrictedProduct
