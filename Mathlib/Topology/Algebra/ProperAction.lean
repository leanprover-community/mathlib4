/-
Copyright (c) 2024 Anatole Dedeker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedeker, Etienne Marion, Florestan Martin-Baillon, Vincent Guirardel
-/
import Mathlib.Topology.Algebra.Group.Basic
import Mathlib.Topology.Algebra.MulAction
import Mathlib.Topology.Maps.Proper.Basic
import Mathlib.Topology.Sequences

/-!
# Proper group action

In this file we define proper action of a group on a topological space, and we prove that in this
case the quotient space is T2. We also give equivalent definitions of proper action using
ultrafilters and show the transfer of proper action to a closed subgroup.
We give sufficent conditions on the topological space such that the action is properly discontinuous
(see `ProperlyDiscontinuousSMul`) if and only if it is continuous in
the first variable (see `ContinuousConstSMul`) and proper in the sense defined here.

## Main definitions

* `ProperSMul` : a group `G` acts properly on a topological space `X`
  if the map `(g, x) ‚Ü¶ (g ‚Ä¢ x, x)` is proper, in the sense of `IsProperMap`.

## Main statements

* `t2Space_quotient_mulAction_of_properSMul`: If a group `G` acts properly
  on a topological space `X`, then the quotient space is Hausdorff (T2).
* `t2Space_of_properSMul_of_t2Group`: If a T2 group acts properly on a topological space,
  then this topological space is T2.
* `properlyDiscontinuousSMul_iff_properSMul`: If a discrete group acts on a T2 space `X` such that
  `X √ó X` is compactly generated, then the action is properly discontinuous if and only if it is
  continuous in the second variable and proper. This in particular true if `X` is locally compact
  or first-countable.

## Implementation notes

Concerning `properlyDiscontinuousSMul_iff_properSMul`, this result should be the only one needed
to link properly discontinuous and proper actions.

TODO: Replace the compactly generated hypothesis by a typeclass instance such that
`WeaklyLocallyCompactSpace.isProperMap_iff_isCompact_preimage` and
`SequentialSpace.isProperMap_iff_isCompact_preimage` are inferred by typeclass inference.

## References

* [N. Bourbaki, *General Topology*][bourbaki1966]

## Tags

Hausdorff, group action, proper action
-/

open Filter Topology Set Prod

/-- Proper group action in the sense of Bourbaki:
the map `G √ó X ‚Üí X √ó X` is a proper map (see `IsProperMap`). -/
class ProperVAdd (G X : Type*) [TopologicalSpace G] [TopologicalSpace X] [AddGroup G]
    [AddAction G X] : Prop where
  /-- Proper group action in the sense of Bourbaki:
  the map `G √ó X ‚Üí X √ó X` is a proper map (see `IsProperMap`). -/
  isProperMap_vadd_pair : IsProperMap (fun gx ‚Ü¶ (gx.1 +·µ• gx.2, gx.2) : G √ó X ‚Üí X √ó X)

/-- Proper group action in the sense of Bourbaki:
the map `G √ó X ‚Üí X √ó X` is a proper map (see `IsProperMap`). -/
@[to_additive existing (attr := mk_iff)]
class ProperSMul (G X : Type*) [TopologicalSpace G] [TopologicalSpace X] [Group G]
    [MulAction G X] : Prop where
  /-- Proper group action in the sense of Bourbaki:
  the map `G √ó X ‚Üí X √ó X` is a proper map (see `IsProperMap`). -/
  isProperMap_smul_pair : IsProperMap (fun gx ‚Ü¶ (gx.1 ‚Ä¢ gx.2, gx.2) : G √ó X ‚Üí X √ó X)

attribute [to_additive existing] properSMul_iff

variable {G X Y Z : Type*} [Group G] [MulAction G X] [MulAction G Y]
variable [TopologicalSpace G] [TopologicalSpace X] [TopologicalSpace Y]
variable [TopologicalSpace Z]

/-- If a group acts properly then in particular it acts continuously. -/
@[to_additive "If a group acts properly then in particular it acts continuously."]
-- See note [lower instance property]
instance (priority := 100) ProperSMul.toContinuousSMul [ProperSMul G X] : ContinuousSMul G X where
  continuous_smul := isProperMap_smul_pair.continuous.fst

/-- A group `G` acts properly on a topological space `X` if and only if for all ultrafilters
`ùí∞` on `X √ó G`, if `ùí∞` converges to `(x‚ÇÅ, x‚ÇÇ)` along the map `(g, x) ‚Ü¶ (g ‚Ä¢ x, x)`,
then there exists `g : G` such that `g ‚Ä¢ x‚ÇÇ = x‚ÇÅ` and `ùí∞.fst` converges to `g`. -/
@[to_additive "A group acts `G` properly on a topological space `X` if and only if
for all ultrafilters `ùí∞` on `X`, if `ùí∞` converges to `(x‚ÇÅ, x‚ÇÇ)`
along the map `(g, x) ‚Ü¶ (g ‚Ä¢ x, x)`, then there exists `g : G` such that `g ‚Ä¢ x‚ÇÇ = x‚ÇÅ`
and `ùí∞.fst` converges to `g`."]
theorem properSMul_iff_continuousSMul_ultrafilter_tendsto :
    ProperSMul G X ‚Üî ContinuousSMul G X ‚àß
      (‚àÄ ùí∞ : Ultrafilter (G √ó X), ‚àÄ x‚ÇÅ x‚ÇÇ : X,
        Tendsto (fun gx : G √ó X ‚Ü¶ (gx.1 ‚Ä¢ gx.2, gx.2)) ùí∞ (ùìù (x‚ÇÅ, x‚ÇÇ)) ‚Üí
      ‚àÉ g : G, g ‚Ä¢ x‚ÇÇ = x‚ÇÅ ‚àß Tendsto (Prod.fst : G √ó X ‚Üí G) ùí∞ (ùìù g)) := by
  refine ‚ü®fun h ‚Ü¶ ‚ü®inferInstance, fun ùí∞ x‚ÇÅ x‚ÇÇ h' ‚Ü¶ ?_‚ü©, fun ‚ü®cont, h‚ü© ‚Ü¶ ?_‚ü©
  ¬∑ rw [properSMul_iff, isProperMap_iff_ultrafilter] at h
    rcases h.2 h' with ‚ü®gx, hgx1, hgx2‚ü©
    refine ‚ü®gx.1, ?_, (continuous_fst.tendsto gx).mono_left hgx2‚ü©
    simp only [Prod.mk.injEq] at hgx1
    rw [‚Üê hgx1.2, hgx1.1]
  ¬∑ rw [properSMul_iff, isProperMap_iff_ultrafilter]
    refine ‚ü®by fun_prop, fun ùí∞ (x‚ÇÅ, x‚ÇÇ) hxx ‚Ü¶ ?_‚ü©
    rcases h ùí∞ x‚ÇÅ x‚ÇÇ hxx with ‚ü®g, hg1, hg2‚ü©
    refine ‚ü®(g, x‚ÇÇ), by simp_rw [hg1], ?_‚ü©
    rw [nhds_prod_eq, ùí∞.le_prod]
    exact ‚ü®hg2, (continuous_snd.tendsto _).comp hxx‚ü©

/-- A group `G` acts properly on a T2 topological space `X` if and only if for all ultrafilters
`ùí∞` on `X √ó G`, if `ùí∞` converges to `(x‚ÇÅ, x‚ÇÇ)` along the map `(g, x) ‚Ü¶ (g ‚Ä¢ x, x)`,
then there exists `g : G` such that `ùí∞.fst` converges to `g`. -/
theorem properSMul_iff_continuousSMul_ultrafilter_tendsto_t2 [T2Space X] :
    ProperSMul G X ‚Üî ContinuousSMul G X ‚àß
      (‚àÄ ùí∞ : Ultrafilter (G √ó X), ‚àÄ x‚ÇÅ x‚ÇÇ : X,
        Tendsto (fun gx : G √ó X ‚Ü¶ (gx.1 ‚Ä¢ gx.2, gx.2)) ùí∞ (ùìù (x‚ÇÅ, x‚ÇÇ)) ‚Üí
     ‚àÉ g : G, Tendsto (Prod.fst : G √ó X ‚Üí G) ùí∞ (ùìù g)) := by
  rw [properSMul_iff_continuousSMul_ultrafilter_tendsto]
  refine and_congr_right fun hc ‚Ü¶ ?_
  congrm ‚àÄ ùí∞ x‚ÇÅ x‚ÇÇ hxx, ‚àÉ g, ?_
  exact and_iff_right_of_imp fun hg ‚Ü¶ tendsto_nhds_unique
    (hg.smul ((continuous_snd.tendsto _).comp hxx)) ((continuous_fst.tendsto _).comp hxx)

/-- If `G` acts properly on `X`, then the quotient space is Hausdorff (T2). -/
@[to_additive "If `G` acts properly on `X`, then the quotient space is Hausdorff (T2)."]
theorem t2Space_quotient_mulAction_of_properSMul [ProperSMul G X] :
    T2Space (Quotient (MulAction.orbitRel G X)) := by
  rw [t2_iff_isClosed_diagonal]
  set R := MulAction.orbitRel G X
  let œÄ : X ‚Üí Quotient R := Quotient.mk'
  have : QuotientMap (Prod.map œÄ œÄ) :=
    (isOpenMap_quotient_mk'_mul.prod isOpenMap_quotient_mk'_mul).to_quotientMap
      (continuous_quotient_mk'.prod_map continuous_quotient_mk')
      ((surjective_quotient_mk' _).prodMap (surjective_quotient_mk' _))
  rw [‚Üê this.isClosed_preimage]
  convert ProperSMul.isProperMap_smul_pair.isClosedMap.isClosed_range
  ¬∑ ext ‚ü®x‚ÇÅ, x‚ÇÇ‚ü©
    simp only [mem_preimage, map_apply, mem_diagonal_iff, mem_range, Prod.mk.injEq, Prod.exists,
      exists_eq_right]
    rw [Quotient.eq_rel, MulAction.orbitRel_apply, MulAction.mem_orbit_iff]
  all_goals infer_instance

/-- If a T2 group acts properly on a topological space, then this topological space is T2. -/
@[to_additive "If a T2 group acts properly on a topological space,
then this topological space is T2."]
theorem t2Space_of_properSMul_of_t2Group [h_proper : ProperSMul G X] [T2Space G] : T2Space X := by
  let f := fun x : X ‚Ü¶ ((1 : G), x)
  have proper_f : IsProperMap f := by
    apply isProperMap_of_closedEmbedding
    rw [closedEmbedding_iff]
    constructor
    ¬∑ let g := fun gx : G √ó X ‚Ü¶ gx.2
      have : Function.LeftInverse g f := fun x ‚Ü¶ by simp
      exact this.embedding (by fun_prop) (by fun_prop)
    ¬∑ have : range f = ({1} √óÀ¢ univ) := by simp
      rw [this]
      exact isClosed_singleton.prod isClosed_univ
  rw [t2_iff_isClosed_diagonal]
  let g := fun gx : G √ó X ‚Ü¶ (gx.1 ‚Ä¢ gx.2, gx.2)
  have proper_g : IsProperMap g := (properSMul_iff G X).1 h_proper
  have : g ‚àò f = fun x ‚Ü¶ (x, x) := by ext x <;> simp
  have range_gf : range (g ‚àò f) = diagonal X := by simp [this]
  rw [‚Üê range_gf]
  exact (proper_f.comp proper_g).isClosed_range

/-- If two groups `H` and `G` act on a topological space `X` such that `G` acts properly and
there exists a group homomorphims `H ‚Üí G` which is a closed embedding compatible with the actions,
then `H` also acts properly on `X`. -/
@[to_additive "If two groups `H` and `G` act on a topological space `X` such that `G` acts properly
and there exists a group homomorphims `H ‚Üí G` which is a closed embedding compatible with the
actions, then `H` also acts properly on `X`."]
theorem properSMul_of_closedEmbedding {H : Type*} [Group H] [MulAction H X] [TopologicalSpace H]
    [ProperSMul G X] (f : H ‚Üí* G) (f_clemb : ClosedEmbedding f)
    (f_compat : ‚àÄ (h : H) (x : X), f h ‚Ä¢ x = h ‚Ä¢ x) : ProperSMul H X where
  isProperMap_smul_pair := by
    have := isProperMap_of_closedEmbedding f_clemb
    have h : IsProperMap (Prod.map f (fun x : X ‚Ü¶ x)) := IsProperMap.prod_map this isProperMap_id
    have : (fun hx : H √ó X ‚Ü¶ (hx.1 ‚Ä¢ hx.2, hx.2)) = (fun hx ‚Ü¶ (f hx.1 ‚Ä¢ hx.2, hx.2)) := by
      simp [f_compat]
    rw [this]
    exact h.comp <| ProperSMul.isProperMap_smul_pair

/-- If `H` is a closed subgroup of `G` and `G` acts properly on X then so does `H`. -/
@[to_additive "If `H` is a closed subgroup of `G` and `G` acts properly on X then so does `H`."]
instance {H : Subgroup G} [ProperSMul G X] [H_closed : IsClosed (H : Set G)] : ProperSMul H X :=
  properSMul_of_closedEmbedding H.subtype H_closed.closedEmbedding_subtype_val fun _ _ ‚Ü¶ rfl

/-- If a discrete group acts on a T2 space `X` such that `X √ó X` is compactly generated,
then the action is properly discontinuous if and only if it is continuous in the second variable
and proper. -/
theorem properlyDiscontinuousSMul_iff_properSMul [T2Space X] [DiscreteTopology G]
    [ContinuousConstSMul G X]
    (compactlyGenerated : ‚àÄ s : Set (X √ó X), IsClosed s ‚Üî ‚àÄ ‚¶ÉK‚¶Ñ, IsCompact K ‚Üí IsClosed (s ‚à© K)) :
    ProperlyDiscontinuousSMul G X ‚Üî ProperSMul G X := by
  constructor
  ¬∑ intro h
    rw [properSMul_iff]
    -- We have to show that `f : (g, x) ‚Ü¶ (g ‚Ä¢ x, x)` is proper.
    -- Continuity follows from continuity of `g ‚Ä¢ ¬∑` and the fact that `G` has the
    -- discrete topology, thanks to `continuous_of_partial_of_discrete`.
    -- Because `X √ó X` is compactly generated, to show that f is proper
    -- it is enough to show that the preimage of a compact set `K` is compact.
    refine (isProperMap_iff_isCompact_preimage compactlyGenerated).2
      ‚ü®(continuous_prod_mk.2
      ‚ü®continuous_prod_of_discrete_left.2 continuous_const_smul, by fun_prop‚ü©),
      fun K hK ‚Ü¶ ?_‚ü©
    -- We set `K' := pr‚ÇÅ(K) ‚à™ pr‚ÇÇ(K)`, which is compact because `K` is compact and `pr‚ÇÅ` and
    -- `pr‚ÇÇ` are continuous. We halso have that `K ‚äÜ K' √ó K'`, and `K` is closed because `X` is T2.
    -- Therefore `f ‚Åª¬π (K)` is also closed and `f ‚Åª¬π (K) ‚äÜ f ‚Åª¬π (K' √ó K')`, thus it suffices to
    -- show that `f ‚Åª¬π (K' √ó K')` is compact.
    let K' := fst '' K ‚à™ snd '' K
    have hK' : IsCompact K' := (hK.image continuous_fst).union (hK.image continuous_snd)
    let E := {g : G | Set.Nonempty ((g ‚Ä¢ ¬∑) '' K' ‚à© K')}
    -- The set `E` is finite because the action is properly discontinuous.
    have fin : Set.Finite E := by
      simp_rw [E, nonempty_iff_ne_empty]
      exact h.finite_disjoint_inter_image hK' hK'
    -- Therefore we can rewrite `f ‚Åª¬π (K' √ó K')` as a finite union of compact sets.
    have : (fun gx : G √ó X ‚Ü¶ (gx.1 ‚Ä¢ gx.2, gx.2)) ‚Åª¬π' (K' √óÀ¢ K') =
        ‚ãÉ g ‚àà E, {g} √óÀ¢ ((g‚Åª¬π ‚Ä¢ ¬∑) '' K' ‚à© K') := by
      ext gx
      simp only [mem_preimage, mem_prod, nonempty_def, mem_inter_iff, mem_image,
        exists_exists_and_eq_and, mem_setOf_eq, singleton_prod, iUnion_exists, biUnion_and',
        mem_iUnion, exists_prop, E]
      constructor
      ¬∑ exact fun ‚ü®gx_mem, x_mem‚ü© ‚Ü¶ ‚ü®gx.2, x_mem, gx.1, gx_mem,
          ‚ü®gx.2, ‚ü®‚ü®gx.1 ‚Ä¢ gx.2, gx_mem, by simp‚ü©, x_mem‚ü©, rfl‚ü©‚ü©
      ¬∑ rintro ‚ü®x, -, g, -, ‚ü®-, ‚ü®‚ü®x', x'_mem, rfl‚ü©, ginvx'_mem‚ü©, rfl‚ü©‚ü©
        exact ‚ü®by simpa, by simpa‚ü©
    -- Indeed each set in this finite union is the product of a singleton and
    -- the intersection of the compact `K'` with its image by some element `g`, and this image is
    -- compact because `g ‚Ä¢ ¬∑` is continuous.
    have : IsCompact ((fun gx : G √ó X ‚Ü¶ (gx.1 ‚Ä¢ gx.2, gx.2)) ‚Åª¬π' (K' √óÀ¢ K')) :=
      this ‚ñ∏ fin.isCompact_biUnion fun g hg ‚Ü¶
        isCompact_singleton.prod <| (hK'.image <| continuous_const_smul _).inter hK'
    -- We conclude as explained above.
    exact this.of_isClosed_subset (hK.isClosed.preimage <|
      continuous_prod_mk.2
      ‚ü®continuous_prod_of_discrete_left.2 continuous_const_smul, by fun_prop‚ü©) <|
      preimage_mono fun x hx ‚Ü¶ ‚ü®Or.inl ‚ü®x, hx, rfl‚ü©, Or.inr ‚ü®x, hx, rfl‚ü©‚ü©
  ¬∑ intro h; constructor
    intro K L hK hL
    simp_rw [‚Üê nonempty_iff_ne_empty]
    -- We want to show that a subset of `G` is finite, but as `G` has the discrete topology it
    -- is enough to show that this subset is compact.
    apply IsCompact.finite_of_discrete
    -- Now set `h : (g, x) ‚Ü¶ (g‚Åª¬π ‚Ä¢ x, x)`, because `f` is proper by hypothesis, so is `h`.
    have : IsProperMap (fun gx : G √ó X ‚Ü¶ (gx.1‚Åª¬π ‚Ä¢ gx.2, gx.2)) :=
      (IsProperMap.prod_map (Homeomorph.isProperMap (Homeomorph.inv G)) isProperMap_id).comp <|
        ProperSMul.isProperMap_smul_pair
    --But we also have that `{g | Set.Nonempty ((g ‚Ä¢ ¬∑) '' K ‚à© L)} = h ‚Åª¬π (K √ó L)`, which
    -- concludes the proof.
    have eq : {g | Set.Nonempty ((g ‚Ä¢ ¬∑) '' K ‚à© L)} =
        fst '' ((fun gx : G √ó X ‚Ü¶ (gx.1‚Åª¬π ‚Ä¢ gx.2, gx.2)) ‚Åª¬π' (K √óÀ¢ L)) := by
      simp_rw [nonempty_def]
      ext g; constructor
      ¬∑ exact fun ‚ü®_, ‚ü®x, x_mem, rfl‚ü©, hx‚ü© ‚Ü¶ ‚ü®(g, g ‚Ä¢ x), ‚ü®by simpa, hx‚ü©, rfl‚ü©
      ¬∑ rintro ‚ü®gx, hgx, rfl‚ü©
        exact ‚ü®gx.2, ‚ü®gx.1‚Åª¬π ‚Ä¢ gx.2, hgx.1, by simp‚ü©, hgx.2‚ü©
    exact eq ‚ñ∏ IsCompact.image (this.isCompact_preimage <| hK.prod hL) continuous_fst

/-- If a discrete group acts on a T2 and locally compact space `X`,
then the action is properly discontinuous if and only if it is continuous in the second variable
and proper. -/
theorem WeaklyLocallyCompactSpace.properlyDiscontinuousSMul_iff_properSMul [T2Space X]
    [WeaklyLocallyCompactSpace X] [DiscreteTopology G] [ContinuousConstSMul G X] :
    ProperlyDiscontinuousSMul G X ‚Üî ProperSMul G X :=
  _root_.properlyDiscontinuousSMul_iff_properSMul
    (fun _ ‚Ü¶ compactlyGenerated_of_weaklyLocallyCompactSpace)

/-- If a discrete group acts on a T2 and first-countable space `X`,
then the action is properly discontinuous if and only if it is continuous in the second variable
and proper. -/
theorem FirstCountableTopology.properlyDiscontinuousSMul_iff_properSMul [T2Space X]
    [FirstCountableTopology X] [DiscreteTopology G] [ContinuousConstSMul G X] :
    ProperlyDiscontinuousSMul G X ‚Üî ProperSMul G X :=
  _root_.properlyDiscontinuousSMul_iff_properSMul (fun _ ‚Ü¶ compactlyGenerated_of_sequentialSpace)
