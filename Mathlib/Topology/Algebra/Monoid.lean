/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Mitchell Lee
-/
import Mathlib.Algebra.BigOperators.Finprod
import Mathlib.Algebra.BigOperators.Pi
import Mathlib.Algebra.Group.Submonoid.Basic
import Mathlib.Algebra.Group.ULift
import Mathlib.Order.Filter.Pointwise
import Mathlib.Topology.Algebra.MulAction
import Mathlib.Topology.ContinuousMap.Defs
import Mathlib.Topology.Algebra.Monoid.Defs

/-!
# Theory of topological monoids

In this file we define mixin classes `ContinuousMul` and `ContinuousAdd`. While in many
applications the underlying type is a monoid (multiplicative or additive), we do not require this in
the definitions.
-/

universe u v

open Set Filter TopologicalSpace Topology
open scoped Topology Pointwise

variable {Î¹ Î± M N X : Type*} [TopologicalSpace X]

@[to_additive (attr := continuity, fun_prop)]
theorem continuous_one [TopologicalSpace M] [One M] : Continuous (1 : X â†’ M) :=
  @continuous_const _ _ _ _ 1

section ContinuousMul

variable [TopologicalSpace M] [Mul M] [ContinuousMul M]

@[to_additive]
instance : ContinuousMul Máµ’áµˆ :=
  â€¹ContinuousMul Mâ€º

@[to_additive]
instance : ContinuousMul (ULift.{u} M) := by
  constructor
  apply continuous_uliftUp.comp
  exact continuous_mul.compâ‚‚ (continuous_uliftDown.comp continuous_fst)
    (continuous_uliftDown.comp continuous_snd)

@[to_additive]
instance ContinuousMul.to_continuousSMul : ContinuousSMul M M :=
  âŸ¨continuous_mulâŸ©

@[to_additive]
instance ContinuousMul.to_continuousSMul_op : ContinuousSMul Máµáµ’áµ– M :=
  âŸ¨show Continuous ((fun p : M Ã— M => p.1 * p.2) âˆ˜ Prod.swap âˆ˜ Prod.map MulOpposite.unop id) from
      continuous_mul.comp <|
        continuous_swap.comp <| Continuous.prodMap MulOpposite.continuous_unop continuous_idâŸ©

@[to_additive]
theorem ContinuousMul.induced {Î± : Type*} {Î² : Type*} {F : Type*} [FunLike F Î± Î²] [Mul Î±]
    [Mul Î²] [MulHomClass F Î± Î²] [tÎ² : TopologicalSpace Î²] [ContinuousMul Î²] (f : F) :
    @ContinuousMul Î± (tÎ².induced f) _ := by
  let tÎ± := tÎ².induced f
  refine âŸ¨continuous_induced_rng.2 ?_âŸ©
  simp only [Function.comp_def, map_mul]
  fun_prop

@[to_additive (attr := continuity)]
theorem continuous_mul_left (a : M) : Continuous fun b : M => a * b :=
  continuous_const.mul continuous_id

@[to_additive (attr := continuity)]
theorem continuous_mul_right (a : M) : Continuous fun b : M => b * a :=
  continuous_id.mul continuous_const

@[to_additive]
theorem tendsto_mul {a b : M} : Tendsto (fun p : M Ã— M => p.fst * p.snd) (ğ“ (a, b)) (ğ“ (a * b)) :=
  continuous_iff_continuousAt.mp ContinuousMul.continuous_mul (a, b)

@[to_additive]
theorem Filter.Tendsto.const_mul (b : M) {c : M} {f : Î± â†’ M} {l : Filter Î±}
    (h : Tendsto (fun k : Î± => f k) l (ğ“ c)) : Tendsto (fun k : Î± => b * f k) l (ğ“ (b * c)) :=
  tendsto_const_nhds.mul h

@[to_additive]
theorem Filter.Tendsto.mul_const (b : M) {c : M} {f : Î± â†’ M} {l : Filter Î±}
    (h : Tendsto (fun k : Î± => f k) l (ğ“ c)) : Tendsto (fun k : Î± => f k * b) l (ğ“ (c * b)) :=
  h.mul tendsto_const_nhds

@[to_additive]
theorem le_nhds_mul (a b : M) : ğ“ a * ğ“ b â‰¤ ğ“ (a * b) := by
  rw [â† mapâ‚‚_mul, â† map_uncurry_prod, â† nhds_prod_eq]
  exact continuous_mul.tendsto _

@[to_additive (attr := simp)]
theorem nhds_one_mul_nhds {M} [MulOneClass M] [TopologicalSpace M] [ContinuousMul M] (a : M) :
    ğ“ (1 : M) * ğ“ a = ğ“ a :=
  ((le_nhds_mul _ _).trans_eq <| congr_arg _ (one_mul a)).antisymm <|
    le_mul_of_one_le_left' <| pure_le_nhds 1

@[to_additive (attr := simp)]
theorem nhds_mul_nhds_one {M} [MulOneClass M] [TopologicalSpace M] [ContinuousMul M] (a : M) :
    ğ“ a * ğ“ 1 = ğ“ a :=
  ((le_nhds_mul _ _).trans_eq <| congr_arg _ (mul_one a)).antisymm <|
    le_mul_of_one_le_right' <| pure_le_nhds 1

section tendsto_nhds

variable {ğ•œ : Type*} [Preorder ğ•œ] [Zero ğ•œ] [Mul ğ•œ] [TopologicalSpace ğ•œ] [ContinuousMul ğ•œ]
  {l : Filter Î±} {f : Î± â†’ ğ•œ} {b c : ğ•œ} (hb : 0 < b)
include hb

theorem Filter.TendstoNhdsWithinIoi.const_mul [PosMulStrictMono ğ•œ] [PosMulReflectLT ğ•œ]
    (h : Tendsto f l (ğ“[>] c)) : Tendsto (fun a => b * f a) l (ğ“[>] (b * c)) :=
  tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within _
      ((tendsto_nhds_of_tendsto_nhdsWithin h).const_mul b) <|
    (tendsto_nhdsWithin_iff.mp h).2.mono fun _ => (mul_lt_mul_left hb).mpr

theorem Filter.TendstoNhdsWithinIio.const_mul [PosMulStrictMono ğ•œ] [PosMulReflectLT ğ•œ]
    (h : Tendsto f l (ğ“[<] c)) : Tendsto (fun a => b * f a) l (ğ“[<] (b * c)) :=
  tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within _
      ((tendsto_nhds_of_tendsto_nhdsWithin h).const_mul b) <|
    (tendsto_nhdsWithin_iff.mp h).2.mono fun _ => (mul_lt_mul_left hb).mpr

theorem Filter.TendstoNhdsWithinIoi.mul_const [MulPosStrictMono ğ•œ] [MulPosReflectLT ğ•œ]
    (h : Tendsto f l (ğ“[>] c)) : Tendsto (fun a => f a * b) l (ğ“[>] (c * b)) :=
  tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within _
      ((tendsto_nhds_of_tendsto_nhdsWithin h).mul_const b) <|
    (tendsto_nhdsWithin_iff.mp h).2.mono fun _ => (mul_lt_mul_right hb).mpr

theorem Filter.TendstoNhdsWithinIio.mul_const [MulPosStrictMono ğ•œ] [MulPosReflectLT ğ•œ]
    (h : Tendsto f l (ğ“[<] c)) : Tendsto (fun a => f a * b) l (ğ“[<] (c * b)) :=
  tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within _
      ((tendsto_nhds_of_tendsto_nhdsWithin h).mul_const b) <|
    (tendsto_nhdsWithin_iff.mp h).2.mono fun _ => (mul_lt_mul_right hb).mpr

end tendsto_nhds

@[to_additive]
protected theorem Specializes.mul {a b c d : M} (hab : a â¤³ b) (hcd : c â¤³ d) : (a * c) â¤³ (b * d) :=
  hab.smul hcd

@[to_additive]
protected theorem Inseparable.mul {a b c d : M} (hab : Inseparable a b) (hcd : Inseparable c d) :
    Inseparable (a * c) (b * d) :=
  hab.smul hcd

@[to_additive]
protected theorem Specializes.pow {M : Type*} [Monoid M] [TopologicalSpace M] [ContinuousMul M]
    {a b : M} (h : a â¤³ b) (n : â„•) : (a ^ n) â¤³ (b ^ n) :=
  Nat.recOn n (by simp only [pow_zero, specializes_rfl]) fun _ ihn â†¦ by
    simpa only [pow_succ] using ihn.mul h

@[to_additive]
protected theorem Inseparable.pow {M : Type*} [Monoid M] [TopologicalSpace M] [ContinuousMul M]
    {a b : M} (h : Inseparable a b) (n : â„•) : Inseparable (a ^ n) (b ^ n) :=
  (h.specializes.pow n).antisymm (h.specializes'.pow n)

/-- Construct a unit from limits of units and their inverses. -/
@[to_additive (attr := simps)
  "Construct an additive unit from limits of additive units and their negatives."]
def Filter.Tendsto.units [TopologicalSpace N] [Monoid N] [ContinuousMul N] [T2Space N]
    {f : Î¹ â†’ NË£} {râ‚ râ‚‚ : N} {l : Filter Î¹} [l.NeBot] (hâ‚ : Tendsto (fun x => â†‘(f x)) l (ğ“ râ‚))
    (hâ‚‚ : Tendsto (fun x => â†‘(f x)â»Â¹) l (ğ“ râ‚‚)) : NË£ where
  val := râ‚
  inv := râ‚‚
  val_inv := by
    symm
    simpa using hâ‚.mul hâ‚‚
  inv_val := by
    symm
    simpa using hâ‚‚.mul hâ‚

@[to_additive]
instance Prod.continuousMul [TopologicalSpace N] [Mul N] [ContinuousMul N] :
    ContinuousMul (M Ã— N) :=
  âŸ¨by apply Continuous.prodMk <;> fun_propâŸ©

@[to_additive]
instance Pi.continuousMul {C : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (C i)] [âˆ€ i, Mul (C i)]
    [âˆ€ i, ContinuousMul (C i)] : ContinuousMul (âˆ€ i, C i) where
  continuous_mul :=
    continuous_pi fun i => (continuous_apply i).fst'.mul (continuous_apply i).snd'

/-- A version of `Pi.continuousMul` for non-dependent functions. It is needed because sometimes
Lean 3 fails to use `Pi.continuousMul` for non-dependent functions. -/
@[to_additive "A version of `Pi.continuousAdd` for non-dependent functions. It is needed
because sometimes Lean fails to use `Pi.continuousAdd` for non-dependent functions."]
instance Pi.continuousMul' : ContinuousMul (Î¹ â†’ M) :=
  Pi.continuousMul

@[to_additive]
instance (priority := 100) continuousMul_of_discreteTopology [TopologicalSpace N] [Mul N]
    [DiscreteTopology N] : ContinuousMul N :=
  âŸ¨continuous_of_discreteTopologyâŸ©

open Filter

open Function

@[to_additive]
theorem ContinuousMul.of_nhds_one {M : Type u} [Monoid M] [TopologicalSpace M]
    (hmul : Tendsto (uncurry ((Â· * Â·) : M â†’ M â†’ M)) (ğ“ 1 Ã—Ë¢ ğ“ 1) <| ğ“ 1)
    (hleft : âˆ€ xâ‚€ : M, ğ“ xâ‚€ = map (fun x => xâ‚€ * x) (ğ“ 1))
    (hright : âˆ€ xâ‚€ : M, ğ“ xâ‚€ = map (fun x => x * xâ‚€) (ğ“ 1)) : ContinuousMul M :=
  âŸ¨by
    rw [continuous_iff_continuousAt]
    rintro âŸ¨xâ‚€, yâ‚€âŸ©
    have key : (fun p : M Ã— M => xâ‚€ * p.1 * (p.2 * yâ‚€)) =
        ((fun x => xâ‚€ * x) âˆ˜ fun x => x * yâ‚€) âˆ˜ uncurry (Â· * Â·) := by
      ext p
      simp [uncurry, mul_assoc]
    have keyâ‚‚ : ((fun x => xâ‚€ * x) âˆ˜ fun x => yâ‚€ * x) = fun x => xâ‚€ * yâ‚€ * x := by
      ext x
      simp [mul_assoc]
    calc
      map (uncurry (Â· * Â·)) (ğ“ (xâ‚€, yâ‚€)) = map (uncurry (Â· * Â·)) (ğ“ xâ‚€ Ã—Ë¢ ğ“ yâ‚€) := by
        rw [nhds_prod_eq]
      _ = map (fun p : M Ã— M => xâ‚€ * p.1 * (p.2 * yâ‚€)) (ğ“ 1 Ã—Ë¢ ğ“ 1) := by
        -- Porting note: `rw` was able to prove this
        -- Now it fails with `failed to rewrite using equation theorems for 'Function.uncurry'`
        -- and `failed to rewrite using equation theorems for 'Function.comp'`.
        -- Removing those two lemmas, the `rw` would succeed, but then needs a `rfl`.
        simp +unfoldPartialApp only [uncurry]
        simp_rw [hleft xâ‚€, hright yâ‚€, prod_map_map_eq, Filter.map_map, Function.comp_def]
      _ = map ((fun x => xâ‚€ * x) âˆ˜ fun x => x * yâ‚€) (map (uncurry (Â· * Â·)) (ğ“ 1 Ã—Ë¢ ğ“ 1)) := by
        rw [key, â† Filter.map_map]
      _ â‰¤ map ((fun x : M => xâ‚€ * x) âˆ˜ fun x => x * yâ‚€) (ğ“ 1) := map_mono hmul
      _ = ğ“ (xâ‚€ * yâ‚€) := by
        rw [â† Filter.map_map, â† hright, hleft yâ‚€, Filter.map_map, keyâ‚‚, â† hleft]âŸ©

@[to_additive]
theorem continuousMul_of_comm_of_nhds_one (M : Type u) [CommMonoid M] [TopologicalSpace M]
    (hmul : Tendsto (uncurry ((Â· * Â·) : M â†’ M â†’ M)) (ğ“ 1 Ã—Ë¢ ğ“ 1) (ğ“ 1))
    (hleft : âˆ€ xâ‚€ : M, ğ“ xâ‚€ = map (fun x => xâ‚€ * x) (ğ“ 1)) : ContinuousMul M := by
  apply ContinuousMul.of_nhds_one hmul hleft
  intro xâ‚€
  simp_rw [mul_comm, hleft xâ‚€]

end ContinuousMul

section PointwiseLimits

variable (Mâ‚ Mâ‚‚ : Type*) [TopologicalSpace Mâ‚‚] [T2Space Mâ‚‚]

@[to_additive]
theorem isClosed_setOf_map_one [One Mâ‚] [One Mâ‚‚] : IsClosed { f : Mâ‚ â†’ Mâ‚‚ | f 1 = 1 } :=
  isClosed_eq (continuous_apply 1) continuous_const

@[to_additive]
theorem isClosed_setOf_map_mul [Mul Mâ‚] [Mul Mâ‚‚] [ContinuousMul Mâ‚‚] :
    IsClosed { f : Mâ‚ â†’ Mâ‚‚ | âˆ€ x y, f (x * y) = f x * f y } := by
  simp only [setOf_forall]
  exact isClosed_iInter fun x â†¦ isClosed_iInter fun y â†¦
    isClosed_eq (continuous_apply _) (by fun_prop)

section Semigroup

variable {Mâ‚ Mâ‚‚} [Mul Mâ‚] [Mul Mâ‚‚] [ContinuousMul Mâ‚‚]
  {F : Type*} [FunLike F Mâ‚ Mâ‚‚] [MulHomClass F Mâ‚ Mâ‚‚] {l : Filter Î±}

/-- Construct a bundled semigroup homomorphism `Mâ‚ â†’â‚™* Mâ‚‚` from a function `f` and a proof that it
belongs to the closure of the range of the coercion from `Mâ‚ â†’â‚™* Mâ‚‚` (or another type of bundled
homomorphisms that has a `MulHomClass` instance) to `Mâ‚ â†’ Mâ‚‚`. -/
@[to_additive (attr := simps -fullyApplied)
  "Construct a bundled additive semigroup homomorphism `Mâ‚ â†’â‚™+ Mâ‚‚` from a function `f`
and a proof that it belongs to the closure of the range of the coercion from `Mâ‚ â†’â‚™+ Mâ‚‚` (or another
type of bundled homomorphisms that has an `AddHomClass` instance) to `Mâ‚ â†’ Mâ‚‚`."]
def mulHomOfMemClosureRangeCoe (f : Mâ‚ â†’ Mâ‚‚)
    (hf : f âˆˆ closure (range fun (f : F) (x : Mâ‚) => f x)) : Mâ‚ â†’â‚™* Mâ‚‚ where
  toFun := f
  map_mul' := (isClosed_setOf_map_mul Mâ‚ Mâ‚‚).closure_subset_iff.2 (range_subset_iff.2 map_mul) hf

/-- Construct a bundled semigroup homomorphism from a pointwise limit of semigroup homomorphisms. -/
@[to_additive (attr := simps! -fullyApplied)
  "Construct a bundled additive semigroup homomorphism from a pointwise limit of additive
semigroup homomorphisms"]
def mulHomOfTendsto (f : Mâ‚ â†’ Mâ‚‚) (g : Î± â†’ F) [l.NeBot]
    (h : Tendsto (fun a x => g a x) l (ğ“ f)) : Mâ‚ â†’â‚™* Mâ‚‚ :=
  mulHomOfMemClosureRangeCoe f <|
    mem_closure_of_tendsto h <| Eventually.of_forall fun _ => mem_range_self _

variable (Mâ‚ Mâ‚‚)

@[to_additive]
theorem MulHom.isClosed_range_coe : IsClosed (Set.range ((â†‘) : (Mâ‚ â†’â‚™* Mâ‚‚) â†’ Mâ‚ â†’ Mâ‚‚)) :=
  isClosed_of_closure_subset fun f hf => âŸ¨mulHomOfMemClosureRangeCoe f hf, rflâŸ©

end Semigroup

section Monoid

variable {Mâ‚ Mâ‚‚} [MulOneClass Mâ‚] [MulOneClass Mâ‚‚] [ContinuousMul Mâ‚‚]
  {F : Type*} [FunLike F Mâ‚ Mâ‚‚] [MonoidHomClass F Mâ‚ Mâ‚‚] {l : Filter Î±}

/-- Construct a bundled monoid homomorphism `Mâ‚ â†’* Mâ‚‚` from a function `f` and a proof that it
belongs to the closure of the range of the coercion from `Mâ‚ â†’* Mâ‚‚` (or another type of bundled
homomorphisms that has a `MonoidHomClass` instance) to `Mâ‚ â†’ Mâ‚‚`. -/
@[to_additive (attr := simps -fullyApplied)
  "Construct a bundled additive monoid homomorphism `Mâ‚ â†’+ Mâ‚‚` from a function `f`
and a proof that it belongs to the closure of the range of the coercion from `Mâ‚ â†’+ Mâ‚‚` (or another
type of bundled homomorphisms that has an `AddMonoidHomClass` instance) to `Mâ‚ â†’ Mâ‚‚`."]
def monoidHomOfMemClosureRangeCoe (f : Mâ‚ â†’ Mâ‚‚)
    (hf : f âˆˆ closure (range fun (f : F) (x : Mâ‚) => f x)) : Mâ‚ â†’* Mâ‚‚ where
  toFun := f
  map_one' := (isClosed_setOf_map_one Mâ‚ Mâ‚‚).closure_subset_iff.2 (range_subset_iff.2 map_one) hf
  map_mul' := (isClosed_setOf_map_mul Mâ‚ Mâ‚‚).closure_subset_iff.2 (range_subset_iff.2 map_mul) hf

/-- Construct a bundled monoid homomorphism from a pointwise limit of monoid homomorphisms. -/
@[to_additive (attr := simps! -fullyApplied)
  "Construct a bundled additive monoid homomorphism from a pointwise limit of additive
monoid homomorphisms"]
def monoidHomOfTendsto (f : Mâ‚ â†’ Mâ‚‚) (g : Î± â†’ F) [l.NeBot]
    (h : Tendsto (fun a x => g a x) l (ğ“ f)) : Mâ‚ â†’* Mâ‚‚ :=
  monoidHomOfMemClosureRangeCoe f <|
    mem_closure_of_tendsto h <| Eventually.of_forall fun _ => mem_range_self _

variable (Mâ‚ Mâ‚‚)

@[to_additive]
theorem MonoidHom.isClosed_range_coe : IsClosed (Set.range ((â†‘) : (Mâ‚ â†’* Mâ‚‚) â†’ Mâ‚ â†’ Mâ‚‚)) :=
  isClosed_of_closure_subset fun f hf => âŸ¨monoidHomOfMemClosureRangeCoe f hf, rflâŸ©

end Monoid

end PointwiseLimits

@[to_additive]
theorem Topology.IsInducing.continuousMul {M N F : Type*} [Mul M] [Mul N] [FunLike F M N]
    [MulHomClass F M N] [TopologicalSpace M] [TopologicalSpace N] [ContinuousMul N] (f : F)
    (hf : IsInducing f) : ContinuousMul M :=
  âŸ¨(hf.continuousSMul hf.continuous (map_mul f _ _)).1âŸ©

@[deprecated (since := "2024-10-28")] alias Inducing.continuousMul := IsInducing.continuousMul

@[to_additive]
theorem continuousMul_induced {M N F : Type*} [Mul M] [Mul N] [FunLike F M N] [MulHomClass F M N]
    [TopologicalSpace N] [ContinuousMul N] (f : F) : @ContinuousMul M (induced f â€¹_â€º) _ :=
  letI := induced f â€¹_â€º
  IsInducing.continuousMul f âŸ¨rflâŸ©

@[to_additive]
instance Subsemigroup.continuousMul [TopologicalSpace M] [Semigroup M] [ContinuousMul M]
    (S : Subsemigroup M) : ContinuousMul S :=
  IsInducing.continuousMul ({ toFun := (â†‘), map_mul' := fun _ _ => rfl} : MulHom S M) âŸ¨rflâŸ©

@[to_additive]
instance Submonoid.continuousMul [TopologicalSpace M] [Monoid M] [ContinuousMul M]
    (S : Submonoid M) : ContinuousMul S :=
  S.toSubsemigroup.continuousMul

section MulZeroClass

open Filter

variable {Î± Î² : Type*}
variable [TopologicalSpace M] [MulZeroClass M] [ContinuousMul M]

theorem exists_mem_nhds_zero_mul_subset
    {K U : Set M} (hK : IsCompact K) (hU : U âˆˆ ğ“ 0) : âˆƒ V âˆˆ ğ“ 0, K * V âŠ† U := by
  refine hK.induction_on ?_ ?_ ?_ ?_
  Â· exact âŸ¨univ, by simpâŸ©
  Â· rintro s t hst âŸ¨V, hV, hV'âŸ©
    exact âŸ¨V, hV, (mul_subset_mul_right hst).trans hV'âŸ©
  Â· rintro s t âŸ¨V, V_in, hV'âŸ© âŸ¨W, W_in, hW'âŸ©
    use V âˆ© W, inter_mem V_in W_in
    rw [union_mul]
    exact
      union_subset ((mul_subset_mul_left V.inter_subset_left).trans hV')
        ((mul_subset_mul_left V.inter_subset_right).trans hW')
  Â· intro x hx
    have := tendsto_mul (show U âˆˆ ğ“ (x * 0) by simpa using hU)
    rw [nhds_prod_eq, mem_map, mem_prod_iff] at this
    rcases this with âŸ¨t, ht, s, hs, hâŸ©
    rw [â† image_subset_iff, image_mul_prod] at h
    exact âŸ¨t, mem_nhdsWithin_of_mem_nhds ht, s, hs, hâŸ©

/-- Let `M` be a topological space with a continuous multiplication operation and a `0`.
Let `l` be a filter on `M` which is disjoint from the cocompact filter. Then, the multiplication map
`M Ã— M â†’ M` tends to zero on the filter product `ğ“ 0 Ã—Ë¢ l`. -/
theorem tendsto_mul_nhds_zero_prod_of_disjoint_cocompact {l : Filter M}
    (hl : Disjoint l (cocompact M)) :
    Tendsto (fun x : M Ã— M â†¦ x.1 * x.2) (ğ“ 0 Ã—Ë¢ l) (ğ“ 0) := calc
  map (fun x : M Ã— M â†¦ x.1 * x.2) (ğ“ 0 Ã—Ë¢ l)
  _ â‰¤ map (fun x : M Ã— M â†¦ x.1 * x.2) (ğ“Ë¢ ({0} Ã—Ë¢ Set.univ)) :=
    map_mono <| nhds_prod_le_of_disjoint_cocompact 0 hl
  _ â‰¤ ğ“ 0 := continuous_mul.tendsto_nhdsSet_nhds fun _ âŸ¨hx, _âŸ© â†¦ mul_eq_zero_of_left hx _

/-- Let `M` be a topological space with a continuous multiplication operation and a `0`.
Let `l` be a filter on `M` which is disjoint from the cocompact filter. Then, the multiplication map
`M Ã— M â†’ M` tends to zero on the filter product `l Ã—Ë¢ ğ“ 0`. -/
theorem tendsto_mul_prod_nhds_zero_of_disjoint_cocompact {l : Filter M}
    (hl : Disjoint l (cocompact M)) :
    Tendsto (fun x : M Ã— M â†¦ x.1 * x.2) (l Ã—Ë¢ ğ“ 0) (ğ“ 0) := calc
  map (fun x : M Ã— M â†¦ x.1 * x.2) (l Ã—Ë¢ ğ“ 0)
  _ â‰¤ map (fun x : M Ã— M â†¦ x.1 * x.2) (ğ“Ë¢ (Set.univ Ã—Ë¢ {0})) :=
    map_mono <| prod_nhds_le_of_disjoint_cocompact 0 hl
  _ â‰¤ ğ“ 0 := continuous_mul.tendsto_nhdsSet_nhds fun _ âŸ¨_, hxâŸ© â†¦ mul_eq_zero_of_right _ hx

/-- Let `M` be a topological space with a continuous multiplication operation and a `0`.
Let `l` be a filter on `M Ã— M` which is disjoint from the cocompact filter. Then, the multiplication
map `M Ã— M â†’ M` tends to zero on `(ğ“ 0).coprod (ğ“ 0) âŠ“ l`. -/
theorem tendsto_mul_coprod_nhds_zero_inf_of_disjoint_cocompact {l : Filter (M Ã— M)}
    (hl : Disjoint l (cocompact (M Ã— M))) :
    Tendsto (fun x : M Ã— M â†¦ x.1 * x.2) ((ğ“ 0).coprod (ğ“ 0) âŠ“ l) (ğ“ 0) := by
  have := calc
    (ğ“ 0).coprod (ğ“ 0) âŠ“ l
    _ â‰¤ (ğ“ 0).coprod (ğ“ 0) âŠ“ map Prod.fst l Ã—Ë¢ map Prod.snd l :=
      inf_le_inf_left _ le_prod_map_fst_snd
    _ â‰¤ ğ“ 0 Ã—Ë¢ map Prod.snd l âŠ” map Prod.fst l Ã—Ë¢ ğ“ 0 :=
      coprod_inf_prod_le _ _ _ _
  apply (Tendsto.sup _ _).mono_left this
  Â· apply tendsto_mul_nhds_zero_prod_of_disjoint_cocompact
    exact disjoint_map_cocompact continuous_snd hl
  Â· apply tendsto_mul_prod_nhds_zero_of_disjoint_cocompact
    exact disjoint_map_cocompact continuous_fst hl

/-- Let `M` be a topological space with a continuous multiplication operation and a `0`.
Let `l` be a filter on `M Ã— M` which is both disjoint from the cocompact filter and less than or
equal to `(ğ“ 0).coprod (ğ“ 0)`. Then the multiplication map `M Ã— M â†’ M` tends to zero on `l`. -/
theorem tendsto_mul_nhds_zero_of_disjoint_cocompact {l : Filter (M Ã— M)}
    (hl : Disjoint l (cocompact (M Ã— M))) (h'l : l â‰¤ (ğ“ 0).coprod (ğ“ 0)) :
    Tendsto (fun x : M Ã— M â†¦ x.1 * x.2) l (ğ“ 0) := by
  simpa [inf_eq_right.mpr h'l] using tendsto_mul_coprod_nhds_zero_inf_of_disjoint_cocompact hl

/-- Let `M` be a topological space with a continuous multiplication operation and a `0`.
Let `f : Î± â†’ M` and `g : Î± â†’ M` be functions. If `f` tends to zero on a filter `l`
and the image of `l` under `g` is disjoint from the cocompact filter on `M`, then
`fun x : Î± â†¦ f x * g x` also tends to zero on `l`. -/
theorem Tendsto.tendsto_mul_zero_of_disjoint_cocompact_right {f g : Î± â†’ M} {l : Filter Î±}
    (hf : Tendsto f l (ğ“ 0)) (hg : Disjoint (map g l) (cocompact M)) :
    Tendsto (fun x â†¦ f x * g x) l (ğ“ 0) :=
  tendsto_mul_nhds_zero_prod_of_disjoint_cocompact hg |>.comp (hf.prodMk tendsto_map)

/-- Let `M` be a topological space with a continuous multiplication operation and a `0`.
Let `f : Î± â†’ M` and `g : Î± â†’ M` be functions. If `g` tends to zero on a filter `l`
and the image of `l` under `f` is disjoint from the cocompact filter on `M`, then
`fun x : Î± â†¦ f x * g x` also tends to zero on `l`. -/
theorem Tendsto.tendsto_mul_zero_of_disjoint_cocompact_left {f g : Î± â†’ M} {l : Filter Î±}
    (hf : Disjoint (map f l) (cocompact M)) (hg : Tendsto g l (ğ“ 0)):
    Tendsto (fun x â†¦ f x * g x) l (ğ“ 0) :=
  tendsto_mul_prod_nhds_zero_of_disjoint_cocompact hf |>.comp (tendsto_map.prodMk hg)

/-- If `f : Î± â†’ M` and `g : Î² â†’ M` are continuous and both tend to zero on the cocompact filter,
then `fun i : Î± Ã— Î² â†¦ f i.1 * g i.2` also tends to zero on the cocompact filter. -/
theorem tendsto_mul_cocompact_nhds_zero [TopologicalSpace Î±] [TopologicalSpace Î²]
    {f : Î± â†’ M} {g : Î² â†’ M} (f_cont : Continuous f) (g_cont : Continuous g)
    (hf : Tendsto f (cocompact Î±) (ğ“ 0)) (hg : Tendsto g (cocompact Î²) (ğ“ 0)) :
    Tendsto (fun i : Î± Ã— Î² â†¦ f i.1 * g i.2) (cocompact (Î± Ã— Î²)) (ğ“ 0) := by
  set l : Filter (M Ã— M) := map (Prod.map f g) (cocompact (Î± Ã— Î²)) with l_def
  set K : Set (M Ã— M) := (insert 0 (range f)) Ã—Ë¢ (insert 0 (range g))
  have K_compact : IsCompact K := .prod (hf.isCompact_insert_range_of_cocompact f_cont)
    (hg.isCompact_insert_range_of_cocompact g_cont)
  have K_mem_l : K âˆˆ l := eventually_map.mpr <| .of_forall fun âŸ¨x, yâŸ© â†¦
    âŸ¨mem_insert_of_mem _ (mem_range_self _), mem_insert_of_mem _ (mem_range_self _)âŸ©
  have l_compact : Disjoint l (cocompact (M Ã— M)) := by
    rw [disjoint_cocompact_right]
    exact âŸ¨K, K_mem_l, K_compactâŸ©
  have l_le_coprod : l â‰¤ (ğ“ 0).coprod (ğ“ 0) := by
    rw [l_def, â† coprod_cocompact]
    exact hf.prodMap_coprod hg
  exact tendsto_mul_nhds_zero_of_disjoint_cocompact l_compact l_le_coprod |>.comp tendsto_map

/-- If `f : Î± â†’ M` and `g : Î² â†’ M` both tend to zero on the cofinite filter, then so does
`fun i : Î± Ã— Î² â†¦ f i.1 * g i.2`. -/
theorem tendsto_mul_cofinite_nhds_zero {f : Î± â†’ M} {g : Î² â†’ M}
    (hf : Tendsto f cofinite (ğ“ 0)) (hg : Tendsto g cofinite (ğ“ 0)) :
    Tendsto (fun i : Î± Ã— Î² â†¦ f i.1 * g i.2) cofinite (ğ“ 0) := by
  letI : TopologicalSpace Î± := âŠ¥
  haveI : DiscreteTopology Î± := discreteTopology_bot Î±
  letI : TopologicalSpace Î² := âŠ¥
  haveI : DiscreteTopology Î² := discreteTopology_bot Î²
  rw [â† cocompact_eq_cofinite] at *
  exact tendsto_mul_cocompact_nhds_zero
    continuous_of_discreteTopology continuous_of_discreteTopology hf hg

end MulZeroClass

section GroupWithZero

lemma GroupWithZero.isOpen_singleton_zero [GroupWithZero M] [TopologicalSpace M]
    [ContinuousMul M] [CompactSpace M] [T1Space M] :
    IsOpen {(0 : M)} := by
  obtain âŸ¨U, hU, h0U, h1UâŸ© := t1Space_iff_exists_open.mp â€¹_â€º zero_ne_one
  obtain âŸ¨W, hW, hW'âŸ© := exists_mem_nhds_zero_mul_subset isCompact_univ (hU.mem_nhds h0U)
  by_cases H : âˆƒ x â‰  0, x âˆˆ W
  Â· obtain âŸ¨x, hx, hxWâŸ© := H
    cases h1U (hW' (by simpa [hx] using Set.mul_mem_mul (Set.mem_univ xâ»Â¹) hxW))
  Â· obtain rfl : W = {0} := subset_antisymm
      (by simpa [not_imp_not] using H) (by simpa using mem_of_mem_nhds hW)
    simpa [isOpen_iff_mem_nhds]

end GroupWithZero

section MulOneClass

variable [TopologicalSpace M] [MulOneClass M] [ContinuousMul M]

@[to_additive exists_open_nhds_zero_half]
theorem exists_open_nhds_one_split {s : Set M} (hs : s âˆˆ ğ“ (1 : M)) :
    âˆƒ V : Set M, IsOpen V âˆ§ (1 : M) âˆˆ V âˆ§ âˆ€ v âˆˆ V, âˆ€ w âˆˆ V, v * w âˆˆ s := by
  have : (fun a : M Ã— M => a.1 * a.2) â»Â¹' s âˆˆ ğ“ ((1, 1) : M Ã— M) :=
    tendsto_mul (by simpa only [one_mul] using hs)
  simpa only [prod_subset_iff] using exists_nhds_square this

@[to_additive exists_nhds_zero_half]
theorem exists_nhds_one_split {s : Set M} (hs : s âˆˆ ğ“ (1 : M)) :
    âˆƒ V âˆˆ ğ“ (1 : M), âˆ€ v âˆˆ V, âˆ€ w âˆˆ V, v * w âˆˆ s :=
  let âŸ¨V, Vo, V1, hVâŸ© := exists_open_nhds_one_split hs
  âŸ¨V, IsOpen.mem_nhds Vo V1, hVâŸ©

/-- Given a neighborhood `U` of `1` there is an open neighborhood `V` of `1`
such that `V * V âŠ† U`. -/
@[to_additive "Given an open neighborhood `U` of `0` there is an open neighborhood `V` of `0`
  such that `V + V âŠ† U`."]
theorem exists_open_nhds_one_mul_subset {U : Set M} (hU : U âˆˆ ğ“ (1 : M)) :
    âˆƒ V : Set M, IsOpen V âˆ§ (1 : M) âˆˆ V âˆ§ V * V âŠ† U := by
  simpa only [mul_subset_iff] using exists_open_nhds_one_split hU

@[to_additive]
theorem Filter.HasBasis.mul_self {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set M} (h : (ğ“ 1).HasBasis p s) :
    (ğ“ 1).HasBasis p fun i => s i * s i := by
  rw [â† nhds_mul_nhds_one, â† mapâ‚‚_mul, â† map_uncurry_prod]
  simpa only [â† image_mul_prod] using h.prod_self.map _

end MulOneClass

section ContinuousMul

section Semigroup

variable [TopologicalSpace M] [Semigroup M] [ContinuousMul M]

@[to_additive]
theorem Subsemigroup.top_closure_mul_self_subset (s : Subsemigroup M) :
    _root_.closure (s : Set M) * _root_.closure s âŠ† _root_.closure s :=
  image2_subset_iff.2 fun _ hx _ hy =>
    map_mem_closureâ‚‚ continuous_mul hx hy fun _ ha _ hb => s.mul_mem ha hb

/-- The (topological-space) closure of a subsemigroup of a space `M` with `ContinuousMul` is
itself a subsemigroup. -/
@[to_additive "The (topological-space) closure of an additive submonoid of a space `M` with
`ContinuousAdd` is itself an additive submonoid."]
def Subsemigroup.topologicalClosure (s : Subsemigroup M) : Subsemigroup M where
  carrier := _root_.closure (s : Set M)
  mul_mem' ha hb := s.top_closure_mul_self_subset âŸ¨_, ha, _, hb, rflâŸ©

@[to_additive]
theorem Subsemigroup.coe_topologicalClosure (s : Subsemigroup M) :
    (s.topologicalClosure : Set M) = _root_.closure (s : Set M) := rfl

@[to_additive]
theorem Subsemigroup.le_topologicalClosure (s : Subsemigroup M) : s â‰¤ s.topologicalClosure :=
  _root_.subset_closure

@[to_additive]
theorem Subsemigroup.isClosed_topologicalClosure (s : Subsemigroup M) :
    IsClosed (s.topologicalClosure : Set M) := isClosed_closure

@[to_additive]
theorem Subsemigroup.topologicalClosure_minimal (s : Subsemigroup M) {t : Subsemigroup M}
    (h : s â‰¤ t) (ht : IsClosed (t : Set M)) : s.topologicalClosure â‰¤ t := closure_minimal h ht

/-- If a subsemigroup of a topological semigroup is commutative, then so is its topological
closure.

See note [reducible non-instances] -/
@[to_additive "If a submonoid of an additive topological monoid is commutative, then so is its
topological closure.

See note [reducible non-instances]"]
abbrev Subsemigroup.commSemigroupTopologicalClosure [T2Space M] (s : Subsemigroup M)
    (hs : âˆ€ x y : s, x * y = y * x) : CommSemigroup s.topologicalClosure :=
  { MulMemClass.toSemigroup s.topologicalClosure with
    mul_comm :=
      have : âˆ€ x âˆˆ s, âˆ€ y âˆˆ s, x * y = y * x := fun x hx y hy =>
        congr_arg Subtype.val (hs âŸ¨x, hxâŸ© âŸ¨y, hyâŸ©)
      fun âŸ¨x, hxâŸ© âŸ¨y, hyâŸ© =>
      Subtype.ext <|
        eqOn_closureâ‚‚ this continuous_mul (continuous_snd.mul continuous_fst) x hx y hy }

@[to_additive]
theorem IsCompact.mul {s t : Set M} (hs : IsCompact s) (ht : IsCompact t) : IsCompact (s * t) := by
  rw [â† image_mul_prod]
  exact (hs.prod ht).image continuous_mul

end Semigroup

variable [TopologicalSpace M] [Monoid M] [ContinuousMul M]

@[to_additive]
theorem Submonoid.top_closure_mul_self_subset (s : Submonoid M) :
    _root_.closure (s : Set M) * _root_.closure s âŠ† _root_.closure s :=
  image2_subset_iff.2 fun _ hx _ hy =>
    map_mem_closureâ‚‚ continuous_mul hx hy fun _ ha _ hb => s.mul_mem ha hb

@[to_additive]
theorem Submonoid.top_closure_mul_self_eq (s : Submonoid M) :
    _root_.closure (s : Set M) * _root_.closure s = _root_.closure s :=
  Subset.antisymm s.top_closure_mul_self_subset fun x hx =>
    âŸ¨x, hx, 1, _root_.subset_closure s.one_mem, mul_one _âŸ©

/-- The (topological-space) closure of a submonoid of a space `M` with `ContinuousMul` is
itself a submonoid. -/
@[to_additive "The (topological-space) closure of an additive submonoid of a space `M` with
`ContinuousAdd` is itself an additive submonoid."]
def Submonoid.topologicalClosure (s : Submonoid M) : Submonoid M where
  carrier := _root_.closure (s : Set M)
  one_mem' := _root_.subset_closure s.one_mem
  mul_mem' ha hb := s.top_closure_mul_self_subset âŸ¨_, ha, _, hb, rflâŸ©

@[to_additive]
theorem Submonoid.coe_topologicalClosure (s : Submonoid M) :
    (s.topologicalClosure : Set M) = _root_.closure (s : Set M) := rfl

@[to_additive]
theorem Submonoid.le_topologicalClosure (s : Submonoid M) : s â‰¤ s.topologicalClosure :=
  _root_.subset_closure

@[to_additive]
theorem Submonoid.isClosed_topologicalClosure (s : Submonoid M) :
    IsClosed (s.topologicalClosure : Set M) := isClosed_closure

@[to_additive]
theorem Submonoid.topologicalClosure_minimal (s : Submonoid M) {t : Submonoid M} (h : s â‰¤ t)
    (ht : IsClosed (t : Set M)) : s.topologicalClosure â‰¤ t := closure_minimal h ht

/-- If a submonoid of a topological monoid is commutative, then so is its topological closure. -/
@[to_additive "If a submonoid of an additive topological monoid is commutative, then so is its
topological closure.

See note [reducible non-instances]."]
abbrev Submonoid.commMonoidTopologicalClosure [T2Space M] (s : Submonoid M)
    (hs : âˆ€ x y : s, x * y = y * x) : CommMonoid s.topologicalClosure :=
  { s.topologicalClosure.toMonoid, s.toSubsemigroup.commSemigroupTopologicalClosure hs with }

@[to_additive exists_nhds_zero_quarter]
theorem exists_nhds_one_split4 {u : Set M} (hu : u âˆˆ ğ“ (1 : M)) :
    âˆƒ V âˆˆ ğ“ (1 : M), âˆ€ {v w s t}, v âˆˆ V â†’ w âˆˆ V â†’ s âˆˆ V â†’ t âˆˆ V â†’ v * w * s * t âˆˆ u := by
  rcases exists_nhds_one_split hu with âŸ¨W, W1, hâŸ©
  rcases exists_nhds_one_split W1 with âŸ¨V, V1, h'âŸ©
  use V, V1
  intro v w s t v_in w_in s_in t_in
  simpa only [mul_assoc] using h _ (h' v v_in w w_in) _ (h' s s_in t t_in)

@[to_additive]
theorem tendsto_list_prod {f : Î¹ â†’ Î± â†’ M} {x : Filter Î±} {a : Î¹ â†’ M} :
    âˆ€ l : List Î¹,
      (âˆ€ i âˆˆ l, Tendsto (f i) x (ğ“ (a i))) â†’
        Tendsto (fun b => (l.map fun c => f c b).prod) x (ğ“ (l.map a).prod)
  | [], _ => by simp [tendsto_const_nhds]
  | f::l, h => by
    simp only [List.map_cons, List.prod_cons]
    exact
      (h f List.mem_cons_self).mul
        (tendsto_list_prod l fun c hc => h c (List.mem_cons_of_mem _ hc))

@[to_additive (attr := continuity)]
theorem continuous_list_prod {f : Î¹ â†’ X â†’ M} (l : List Î¹) (h : âˆ€ i âˆˆ l, Continuous (f i)) :
    Continuous fun a => (l.map fun i => f i a).prod :=
  continuous_iff_continuousAt.2 fun x =>
    tendsto_list_prod l fun c hc => continuous_iff_continuousAt.1 (h c hc) x

@[to_additive]
theorem continuousOn_list_prod {f : Î¹ â†’ X â†’ M} (l : List Î¹) {t : Set X}
    (h : âˆ€ i âˆˆ l, ContinuousOn (f i) t) :
    ContinuousOn (fun a => (l.map fun i => f i a).prod) t := by
  intro x hx
  rw [continuousWithinAt_iff_continuousAt_restrict _ hx]
  refine tendsto_list_prod _ fun i hi => ?_
  specialize h i hi x hx
  rw [continuousWithinAt_iff_continuousAt_restrict _ hx] at h
  exact h

@[to_additive (attr := continuity)]
theorem continuous_pow : âˆ€ n : â„•, Continuous fun a : M => a ^ n
  | 0 => by simpa using continuous_const
  | k + 1 => by
    simp only [pow_succ']
    exact continuous_id.mul (continuous_pow _)

instance AddMonoid.continuousConstSMul_nat {A} [AddMonoid A] [TopologicalSpace A]
    [ContinuousAdd A] : ContinuousConstSMul â„• A :=
  âŸ¨continuous_nsmulâŸ©

instance AddMonoid.continuousSMul_nat {A} [AddMonoid A] [TopologicalSpace A]
    [ContinuousAdd A] : ContinuousSMul â„• A :=
  âŸ¨continuous_prod_of_discrete_left.mpr continuous_nsmulâŸ©

-- We register `Continuous.pow` as a `continuity` lemma with low penalty (so
-- `continuity` will try it before other `continuity` lemmas). This is a
-- workaround for goals of the form `Continuous fun x => x ^ 2`, where
-- `continuity` applies `Continuous.mul` since the goal is defeq to
-- `Continuous fun x => x * x`.
--
-- To properly fix this, we should make sure that `continuity` applies its
-- lemmas with reducible transparency, preventing the unfolding of `^`. But this
-- is quite an invasive change.
@[to_additive (attr := aesop safe -100 (rule_sets := [Continuous]), fun_prop)]
theorem Continuous.pow {f : X â†’ M} (h : Continuous f) (n : â„•) : Continuous fun b => f b ^ n :=
  (continuous_pow n).comp h

@[to_additive]
theorem continuousOn_pow {s : Set M} (n : â„•) : ContinuousOn (fun (x : M) => x ^ n) s :=
  (continuous_pow n).continuousOn

@[to_additive]
theorem continuousAt_pow (x : M) (n : â„•) : ContinuousAt (fun (x : M) => x ^ n) x :=
  (continuous_pow n).continuousAt

@[to_additive]
theorem Filter.Tendsto.pow {l : Filter Î±} {f : Î± â†’ M} {x : M} (hf : Tendsto f l (ğ“ x)) (n : â„•) :
    Tendsto (fun x => f x ^ n) l (ğ“ (x ^ n)) :=
  (continuousAt_pow _ _).tendsto.comp hf

@[to_additive]
theorem ContinuousWithinAt.pow {f : X â†’ M} {x : X} {s : Set X} (hf : ContinuousWithinAt f s x)
    (n : â„•) : ContinuousWithinAt (fun x => f x ^ n) s x :=
  Filter.Tendsto.pow hf n

@[to_additive (attr := fun_prop)]
theorem ContinuousAt.pow {f : X â†’ M} {x : X} (hf : ContinuousAt f x) (n : â„•) :
    ContinuousAt (fun x => f x ^ n) x :=
  Filter.Tendsto.pow hf n

@[to_additive (attr := fun_prop)]
theorem ContinuousOn.pow {f : X â†’ M} {s : Set X} (hf : ContinuousOn f s) (n : â„•) :
    ContinuousOn (fun x => f x ^ n) s := fun x hx => (hf x hx).pow n

/-- Left-multiplication by a left-invertible element of a topological monoid is proper, i.e.,
inverse images of compact sets are compact. -/
theorem Filter.tendsto_cocompact_mul_left {a b : M} (ha : b * a = 1) :
    Filter.Tendsto (fun x : M => a * x) (Filter.cocompact M) (Filter.cocompact M) := by
  refine Filter.Tendsto.of_tendsto_comp ?_ (Filter.comap_cocompact_le (continuous_mul_left b))
  convert Filter.tendsto_id
  ext x
  simp [â† mul_assoc, ha]

/-- Right-multiplication by a right-invertible element of a topological monoid is proper, i.e.,
inverse images of compact sets are compact. -/
theorem Filter.tendsto_cocompact_mul_right {a b : M} (ha : a * b = 1) :
    Filter.Tendsto (fun x : M => x * a) (Filter.cocompact M) (Filter.cocompact M) := by
  refine Filter.Tendsto.of_tendsto_comp ?_ (Filter.comap_cocompact_le (continuous_mul_right b))
  simp only [comp_mul_right, ha, mul_one]
  exact Filter.tendsto_id

/-- If `R` acts on `A` via `A`, then continuous multiplication implies continuous scalar
multiplication by constants.

Notably, this instances applies when `R = A`, or when `[Algebra R A]` is available. -/
@[to_additive "If `R` acts on `A` via `A`, then continuous addition implies
continuous affine addition by constants."]
instance (priority := 100) IsScalarTower.continuousConstSMul {R A : Type*} [Monoid A] [SMul R A]
    [IsScalarTower R A A] [TopologicalSpace A] [ContinuousMul A] : ContinuousConstSMul R A where
  continuous_const_smul q := by
    simp +singlePass only [â† smul_one_mul q (_ : A)]
    exact continuous_const.mul continuous_id

/-- If the action of `R` on `A` commutes with left-multiplication, then continuous multiplication
implies continuous scalar multiplication by constants.

Notably, this instances applies when `R = Aáµáµ’áµ–`. -/
@[to_additive "If the action of `R` on `A` commutes with left-addition, then
continuous addition implies continuous affine addition by constants.

Notably, this instances applies when `R = Aáµƒáµ’áµ–`."]
instance (priority := 100) SMulCommClass.continuousConstSMul {R A : Type*} [Monoid A] [SMul R A]
    [SMulCommClass R A A] [TopologicalSpace A] [ContinuousMul A] : ContinuousConstSMul R A where
  continuous_const_smul q := by
    simp +singlePass only [â† mul_smul_one q (_ : A)]
    exact continuous_id.mul continuous_const

end ContinuousMul

namespace MulOpposite

/-- If multiplication is continuous in `Î±`, then it also is in `Î±áµáµ’áµ–`. -/
@[to_additive "If addition is continuous in `Î±`, then it also is in `Î±áµƒáµ’áµ–`."]
instance [TopologicalSpace Î±] [Mul Î±] [ContinuousMul Î±] : ContinuousMul Î±áµáµ’áµ– :=
  âŸ¨continuous_op.comp (continuous_unop.snd'.mul continuous_unop.fst')âŸ©

end MulOpposite

namespace Units

open MulOpposite

variable [TopologicalSpace Î±] [Monoid Î±] [ContinuousMul Î±]

/-- If multiplication on a monoid is continuous, then multiplication on the units of the monoid,
with respect to the induced topology, is continuous.

Inversion is also continuous, but we register this in a later file, `Topology.Algebra.Group`,
because the predicate `ContinuousInv` has not yet been defined. -/
@[to_additive "If addition on an additive monoid is continuous, then addition on the additive units
of the monoid, with respect to the induced topology, is continuous.

Negation is also continuous, but we register this in a later file, `Topology.Algebra.Group`, because
the predicate `ContinuousNeg` has not yet been defined."]
instance : ContinuousMul Î±Ë£ := isInducing_embedProduct.continuousMul (embedProduct Î±)

end Units

@[to_additive (attr := fun_prop)]
theorem Continuous.units_map [Monoid M] [Monoid N] [TopologicalSpace M] [TopologicalSpace N]
    (f : M â†’* N) (hf : Continuous f) : Continuous (Units.map f) :=
  Units.continuous_iff.2 âŸ¨hf.comp Units.continuous_val, hf.comp Units.continuous_coe_invâŸ©

section

variable [TopologicalSpace M] [CommMonoid M]

@[to_additive]
theorem Submonoid.mem_nhds_one (S : Submonoid M) (oS : IsOpen (S : Set M)) :
    (S : Set M) âˆˆ ğ“ (1 : M) :=
  IsOpen.mem_nhds oS S.one_mem

variable [ContinuousMul M]

@[to_additive]
theorem tendsto_multiset_prod {f : Î¹ â†’ Î± â†’ M} {x : Filter Î±} {a : Î¹ â†’ M} (s : Multiset Î¹) :
    (âˆ€ i âˆˆ s, Tendsto (f i) x (ğ“ (a i))) â†’
      Tendsto (fun b => (s.map fun c => f c b).prod) x (ğ“ (s.map a).prod) := by
  rcases s with âŸ¨lâŸ©
  simpa using tendsto_list_prod l

@[to_additive]
theorem tendsto_finset_prod {f : Î¹ â†’ Î± â†’ M} {x : Filter Î±} {a : Î¹ â†’ M} (s : Finset Î¹) :
    (âˆ€ i âˆˆ s, Tendsto (f i) x (ğ“ (a i))) â†’
      Tendsto (fun b => âˆ c âˆˆ s, f c b) x (ğ“ (âˆ c âˆˆ s, a c)) :=
  tendsto_multiset_prod _

@[to_additive (attr := continuity)]
theorem continuous_multiset_prod {f : Î¹ â†’ X â†’ M} (s : Multiset Î¹) :
    (âˆ€ i âˆˆ s, Continuous (f i)) â†’ Continuous fun a => (s.map fun i => f i a).prod := by
  rcases s with âŸ¨lâŸ©
  simpa using continuous_list_prod l

@[to_additive]
theorem continuousOn_multiset_prod {f : Î¹ â†’ X â†’ M} (s : Multiset Î¹) {t : Set X} :
    (âˆ€ i âˆˆ s, ContinuousOn (f i) t) â†’ ContinuousOn (fun a => (s.map fun i => f i a).prod) t := by
  rcases s with âŸ¨lâŸ©
  simpa using continuousOn_list_prod l

@[to_additive (attr := continuity, fun_prop)]
theorem continuous_finset_prod {f : Î¹ â†’ X â†’ M} (s : Finset Î¹) :
    (âˆ€ i âˆˆ s, Continuous (f i)) â†’ Continuous fun a => âˆ i âˆˆ s, f i a :=
  continuous_multiset_prod _

@[to_additive]
theorem continuousOn_finset_prod {f : Î¹ â†’ X â†’ M} (s : Finset Î¹) {t : Set X} :
    (âˆ€ i âˆˆ s, ContinuousOn (f i) t) â†’ ContinuousOn (fun a => âˆ i âˆˆ s, f i a) t :=
  continuousOn_multiset_prod _

@[to_additive]
theorem eventuallyEq_prod {X M : Type*} [CommMonoid M] {s : Finset Î¹} {l : Filter X}
    {f g : Î¹ â†’ X â†’ M} (hs : âˆ€ i âˆˆ s, f i =á¶ [l] g i) : âˆ i âˆˆ s, f i =á¶ [l] âˆ i âˆˆ s, g i := by
  replace hs : âˆ€á¶  x in l, âˆ€ i âˆˆ s, f i x = g i x := by rwa [eventually_all_finset]
  filter_upwards [hs] with x hx
  simp only [Finset.prod_apply, Finset.prod_congr rfl hx]

open Function

@[to_additive]
theorem LocallyFinite.exists_finset_mulSupport {M : Type*} [One M] {f : Î¹ â†’ X â†’ M}
    (hf : LocallyFinite fun i => mulSupport <| f i) (xâ‚€ : X) :
    âˆƒ I : Finset Î¹, âˆ€á¶  x in ğ“ xâ‚€, (mulSupport fun i => f i x) âŠ† I := by
  rcases hf xâ‚€ with âŸ¨U, hxU, hUfâŸ©
  refine âŸ¨hUf.toFinset, mem_of_superset hxU fun y hy i hi => ?_âŸ©
  rw [hUf.coe_toFinset]
  exact âŸ¨y, hi, hyâŸ©

@[to_additive]
theorem finprod_eventually_eq_prod {M : Type*} [CommMonoid M] {f : Î¹ â†’ X â†’ M}
    (hf : LocallyFinite fun i => mulSupport (f i)) (x : X) :
    âˆƒ s : Finset Î¹, âˆ€á¶  y in ğ“ x, âˆá¶  i, f i y = âˆ i âˆˆ s, f i y :=
  let âŸ¨I, hIâŸ© := hf.exists_finset_mulSupport x
  âŸ¨I, hI.mono fun _ hy => finprod_eq_prod_of_mulSupport_subset _ fun _ hi => hy hiâŸ©

@[to_additive]
theorem continuous_finprod {f : Î¹ â†’ X â†’ M} (hc : âˆ€ i, Continuous (f i))
    (hf : LocallyFinite fun i => mulSupport (f i)) : Continuous fun x => âˆá¶  i, f i x := by
  refine continuous_iff_continuousAt.2 fun x => ?_
  rcases finprod_eventually_eq_prod hf x with âŸ¨s, hsâŸ©
  refine ContinuousAt.congr ?_ (EventuallyEq.symm hs)
  exact tendsto_finset_prod _ fun i _ => (hc i).continuousAt

@[to_additive]
theorem continuous_finprod_cond {f : Î¹ â†’ X â†’ M} {p : Î¹ â†’ Prop} (hc : âˆ€ i, p i â†’ Continuous (f i))
    (hf : LocallyFinite fun i => mulSupport (f i)) :
    Continuous fun x => âˆá¶  (i) (_ : p i), f i x := by
  simp only [â† finprod_subtype_eq_finprod_cond]
  exact continuous_finprod (fun i => hc i i.2) (hf.comp_injective Subtype.coe_injective)

end

instance [TopologicalSpace M] [Mul M] [ContinuousMul M] : ContinuousAdd (Additive M) where
  continuous_add := @continuous_mul M _ _ _

instance [TopologicalSpace M] [Add M] [ContinuousAdd M] : ContinuousMul (Multiplicative M) where
  continuous_mul := @continuous_add M _ _ _

section LatticeOps

variable {Î¹' : Sort*} [Mul M]

@[to_additive]
theorem continuousMul_sInf {ts : Set (TopologicalSpace M)}
    (h : âˆ€ t âˆˆ ts, @ContinuousMul M t _) : @ContinuousMul M (sInf ts) _ :=
  letI := sInf ts
  { continuous_mul :=
      continuous_sInf_rng.2 fun t ht =>
        continuous_sInf_domâ‚‚ ht ht (@ContinuousMul.continuous_mul M t _ (h t ht)) }

@[to_additive]
theorem continuousMul_iInf {ts : Î¹' â†’ TopologicalSpace M}
    (h' : âˆ€ i, @ContinuousMul M (ts i) _) : @ContinuousMul M (â¨… i, ts i) _ := by
  rw [â† sInf_range]
  exact continuousMul_sInf (Set.forall_mem_range.mpr h')

@[to_additive]
theorem continuousMul_inf {tâ‚ tâ‚‚ : TopologicalSpace M} (hâ‚ : @ContinuousMul M tâ‚ _)
    (hâ‚‚ : @ContinuousMul M tâ‚‚ _) : @ContinuousMul M (tâ‚ âŠ“ tâ‚‚) _ := by
  rw [inf_eq_iInf]
  refine continuousMul_iInf fun b => ?_
  cases b <;> assumption

end LatticeOps

namespace ContinuousMap

variable [Mul X] [ContinuousMul X]

/-- The continuous map `fun y => y * x` -/
@[to_additive "The continuous map `fun y => y + x`"]
protected def mulRight (x : X) : C(X, X) :=
  mk _ (continuous_mul_right x)

@[to_additive (attr := simp)]
theorem coe_mulRight (x : X) : â‡‘(ContinuousMap.mulRight x) = fun y => y * x :=
  rfl

/-- The continuous map `fun y => x * y` -/
@[to_additive "The continuous map `fun y => x + y`"]
protected def mulLeft (x : X) : C(X, X) :=
  mk _ (continuous_mul_left x)

@[to_additive (attr := simp)]
theorem coe_mulLeft (x : X) : â‡‘(ContinuousMap.mulLeft x) = fun y => x * y :=
  rfl

end ContinuousMap
