/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro, Mitchell Lee
-/
import Mathlib.Algebra.BigOperators.Finprod
import Mathlib.Algebra.BigOperators.Pi
import Mathlib.Algebra.Group.Submonoid.Basic
import Mathlib.Algebra.Group.ULift
import Mathlib.Order.Filter.Pointwise
import Mathlib.Topology.Algebra.MulAction
import Mathlib.Topology.ContinuousMap.Defs
import Mathlib.Topology.Algebra.Monoid.Defs

/-!
# Theory of topological monoids

In this file we define mixin classes `ContinuousMul` and `ContinuousAdd`. While in many
applications the underlying type is a monoid (multiplicative or additive), we do not require this in
the definitions.
-/

universe u v

open Set Filter TopologicalSpace Topology
open scoped Topology Pointwise

variable {Œπ Œ± M N X : Type*} [TopologicalSpace X]

@[to_additive (attr := continuity, fun_prop)]
theorem continuous_one [TopologicalSpace M] [One M] : Continuous (1 : X ‚Üí M) :=
  @continuous_const _ _ _ _ 1

section ContinuousMul

variable [TopologicalSpace M] [Mul M] [ContinuousMul M]

@[to_additive]
instance : ContinuousMul M·µí·µà :=
  ‚ÄπContinuousMul M‚Ä∫

@[to_additive]
instance : ContinuousMul (ULift.{u} M) := by
  constructor
  apply continuous_uliftUp.comp
  exact continuous_mul.comp‚ÇÇ (continuous_uliftDown.comp continuous_fst)
    (continuous_uliftDown.comp continuous_snd)

@[to_additive]
instance ContinuousMul.to_continuousSMul : ContinuousSMul M M :=
  ‚ü®continuous_mul‚ü©

@[to_additive]
instance ContinuousMul.to_continuousSMul_op : ContinuousSMul M·µê·µí·µñ M :=
  ‚ü®show Continuous ((fun p : M √ó M ‚Ü¶ p.1 * p.2) ‚àò Prod.swap ‚àò Prod.map MulOpposite.unop id) from
      continuous_mul.comp <|
        continuous_swap.comp <| Continuous.prodMap MulOpposite.continuous_unop continuous_id‚ü©

@[to_additive]
theorem ContinuousMul.induced {Œ± : Type*} {Œ≤ : Type*} {F : Type*} [FunLike F Œ± Œ≤] [Mul Œ±]
    [Mul Œ≤] [MulHomClass F Œ± Œ≤] [tŒ≤ : TopologicalSpace Œ≤] [ContinuousMul Œ≤] (f : F) :
    @ContinuousMul Œ± (tŒ≤.induced f) _ := by
  let tŒ± := tŒ≤.induced f
  refine ‚ü®continuous_induced_rng.2 ?_‚ü©
  simp only [Function.comp_def, map_mul]
  fun_prop

@[to_additive (attr := continuity)]
theorem continuous_mul_left (a : M) : Continuous fun b : M ‚Ü¶ a * b :=
  continuous_const.mul continuous_id

@[to_additive (attr := continuity)]
theorem continuous_mul_right (a : M) : Continuous fun b : M ‚Ü¶ b * a :=
  continuous_id.mul continuous_const

@[to_additive]
theorem tendsto_mul {a b : M} : Tendsto (fun p : M √ó M ‚Ü¶ p.fst * p.snd) (ùìù (a, b)) (ùìù (a * b)) :=
  continuous_iff_continuousAt.mp ContinuousMul.continuous_mul (a, b)

@[to_additive]
theorem Filter.Tendsto.const_mul (b : M) {c : M} {f : Œ± ‚Üí M} {l : Filter Œ±}
    (h : Tendsto (fun k : Œ± ‚Ü¶ f k) l (ùìù c)) : Tendsto (fun k : Œ± ‚Ü¶ b * f k) l (ùìù (b * c)) :=
  tendsto_const_nhds.mul h

@[to_additive]
theorem Filter.Tendsto.mul_const (b : M) {c : M} {f : Œ± ‚Üí M} {l : Filter Œ±}
    (h : Tendsto (fun k : Œ± ‚Ü¶ f k) l (ùìù c)) : Tendsto (fun k : Œ± ‚Ü¶ f k * b) l (ùìù (c * b)) :=
  h.mul tendsto_const_nhds

@[to_additive]
theorem le_nhds_mul (a b : M) : ùìù a * ùìù b ‚â§ ùìù (a * b) := by
  rw [‚Üê map‚ÇÇ_mul, ‚Üê map_uncurry_prod, ‚Üê nhds_prod_eq]
  exact continuous_mul.tendsto _

@[to_additive (attr := simp)]
theorem nhds_one_mul_nhds {M} [MulOneClass M] [TopologicalSpace M] [ContinuousMul M] (a : M) :
    ùìù (1 : M) * ùìù a = ùìù a :=
  ((le_nhds_mul _ _).trans_eq <| congr_arg _ (one_mul a)).antisymm <|
    le_mul_of_one_le_left' <| pure_le_nhds 1

@[to_additive (attr := simp)]
theorem nhds_mul_nhds_one {M} [MulOneClass M] [TopologicalSpace M] [ContinuousMul M] (a : M) :
    ùìù a * ùìù 1 = ùìù a :=
  ((le_nhds_mul _ _).trans_eq <| congr_arg _ (mul_one a)).antisymm <|
    le_mul_of_one_le_right' <| pure_le_nhds 1

section tendsto_nhds

variable {ùïú : Type*} [Preorder ùïú] [Zero ùïú] [Mul ùïú] [TopologicalSpace ùïú] [ContinuousMul ùïú]
  {l : Filter Œ±} {f : Œ± ‚Üí ùïú} {b c : ùïú} (hb : 0 < b)
include hb

theorem Filter.TendstoNhdsWithinIoi.const_mul [PosMulStrictMono ùïú] [PosMulReflectLT ùïú]
    (h : Tendsto f l (ùìù[>] c)) : Tendsto (fun a ‚Ü¶ b * f a) l (ùìù[>] (b * c)) :=
  tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within _
      ((tendsto_nhds_of_tendsto_nhdsWithin h).const_mul b) <|
    (tendsto_nhdsWithin_iff.mp h).2.mono fun _ ‚Ü¶ (mul_lt_mul_left hb).mpr

theorem Filter.TendstoNhdsWithinIio.const_mul [PosMulStrictMono ùïú] [PosMulReflectLT ùïú]
    (h : Tendsto f l (ùìù[<] c)) : Tendsto (fun a ‚Ü¶ b * f a) l (ùìù[<] (b * c)) :=
  tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within _
      ((tendsto_nhds_of_tendsto_nhdsWithin h).const_mul b) <|
    (tendsto_nhdsWithin_iff.mp h).2.mono fun _ ‚Ü¶ (mul_lt_mul_left hb).mpr

theorem Filter.TendstoNhdsWithinIoi.mul_const [MulPosStrictMono ùïú] [MulPosReflectLT ùïú]
    (h : Tendsto f l (ùìù[>] c)) : Tendsto (fun a ‚Ü¶ f a * b) l (ùìù[>] (c * b)) :=
  tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within _
      ((tendsto_nhds_of_tendsto_nhdsWithin h).mul_const b) <|
    (tendsto_nhdsWithin_iff.mp h).2.mono fun _ ‚Ü¶ (mul_lt_mul_right hb).mpr

theorem Filter.TendstoNhdsWithinIio.mul_const [MulPosStrictMono ùïú] [MulPosReflectLT ùïú]
    (h : Tendsto f l (ùìù[<] c)) : Tendsto (fun a ‚Ü¶ f a * b) l (ùìù[<] (c * b)) :=
  tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within _
      ((tendsto_nhds_of_tendsto_nhdsWithin h).mul_const b) <|
    (tendsto_nhdsWithin_iff.mp h).2.mono fun _ ‚Ü¶ (mul_lt_mul_right hb).mpr

end tendsto_nhds

@[to_additive]
protected theorem Specializes.mul {a b c d : M} (hab : a ‚§≥ b) (hcd : c ‚§≥ d) : (a * c) ‚§≥ (b * d) :=
  hab.smul hcd

@[to_additive]
protected theorem Inseparable.mul {a b c d : M} (hab : Inseparable a b) (hcd : Inseparable c d) :
    Inseparable (a * c) (b * d) :=
  hab.smul hcd

@[to_additive]
protected theorem Specializes.pow {M : Type*} [Monoid M] [TopologicalSpace M] [ContinuousMul M]
    {a b : M} (h : a ‚§≥ b) (n : ‚Ñï) : (a ^ n) ‚§≥ (b ^ n) :=
  Nat.recOn n (by simp only [pow_zero, specializes_rfl]) fun _ ihn ‚Ü¶ by
    simpa only [pow_succ] using ihn.mul h

@[to_additive]
protected theorem Inseparable.pow {M : Type*} [Monoid M] [TopologicalSpace M] [ContinuousMul M]
    {a b : M} (h : Inseparable a b) (n : ‚Ñï) : Inseparable (a ^ n) (b ^ n) :=
  (h.specializes.pow n).antisymm (h.specializes'.pow n)

/-- Construct a unit from limits of units and their inverses. -/
@[to_additive (attr := simps)
  /-- Construct an additive unit from limits of additive units and their negatives. -/]
def Filter.Tendsto.units [TopologicalSpace N] [Monoid N] [ContinuousMul N] [T2Space N]
    {f : Œπ ‚Üí NÀ£} {r‚ÇÅ r‚ÇÇ : N} {l : Filter Œπ} [l.NeBot] (h‚ÇÅ : Tendsto (fun x ‚Ü¶ ‚Üë(f x)) l (ùìù r‚ÇÅ))
    (h‚ÇÇ : Tendsto (fun x ‚Ü¶ ‚Üë(f x)‚Åª¬π) l (ùìù r‚ÇÇ)) : NÀ£ where
  val := r‚ÇÅ
  inv := r‚ÇÇ
  val_inv := by
    symm
    simpa using h‚ÇÅ.mul h‚ÇÇ
  inv_val := by
    symm
    simpa using h‚ÇÇ.mul h‚ÇÅ

@[to_additive]
instance Prod.continuousMul [TopologicalSpace N] [Mul N] [ContinuousMul N] :
    ContinuousMul (M √ó N) :=
  ‚ü®by apply Continuous.prodMk <;> fun_prop‚ü©

@[to_additive]
instance Pi.continuousMul {C : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (C i)] [‚àÄ i, Mul (C i)]
    [‚àÄ i, ContinuousMul (C i)] : ContinuousMul (‚àÄ i, C i) where
  continuous_mul :=
    continuous_pi fun i ‚Ü¶ (continuous_apply i).fst'.mul (continuous_apply i).snd'

/-- A version of `Pi.continuousMul` for non-dependent functions. It is needed because sometimes
Lean 3 fails to use `Pi.continuousMul` for non-dependent functions. -/
@[to_additive /-- A version of `Pi.continuousAdd` for non-dependent functions. It is needed
because sometimes Lean fails to use `Pi.continuousAdd` for non-dependent functions. -/]
instance Pi.continuousMul' : ContinuousMul (Œπ ‚Üí M) :=
  Pi.continuousMul

@[to_additive]
instance (priority := 100) continuousMul_of_discreteTopology [TopologicalSpace N] [Mul N]
    [DiscreteTopology N] : ContinuousMul N :=
  ‚ü®continuous_of_discreteTopology‚ü©

open Filter

open Function

@[to_additive]
theorem ContinuousMul.of_nhds_one {M : Type u} [Monoid M] [TopologicalSpace M]
    (hmul : Tendsto (uncurry ((¬∑ * ¬∑) : M ‚Üí M ‚Üí M)) (ùìù 1 √óÀ¢ ùìù 1) <| ùìù 1)
    (hleft : ‚àÄ x‚ÇÄ : M, ùìù x‚ÇÄ = map (fun x ‚Ü¶ x‚ÇÄ * x) (ùìù 1))
    (hright : ‚àÄ x‚ÇÄ : M, ùìù x‚ÇÄ = map (fun x ‚Ü¶ x * x‚ÇÄ) (ùìù 1)) : ContinuousMul M :=
  ‚ü®by
    rw [continuous_iff_continuousAt]
    rintro ‚ü®x‚ÇÄ, y‚ÇÄ‚ü©
    have key : (fun p : M √ó M ‚Ü¶ x‚ÇÄ * p.1 * (p.2 * y‚ÇÄ)) =
        ((fun x ‚Ü¶ x‚ÇÄ * x) ‚àò fun x ‚Ü¶ x * y‚ÇÄ) ‚àò uncurry (¬∑ * ¬∑) := by
      ext p
      simp [uncurry, mul_assoc]
    have key‚ÇÇ : ((fun x ‚Ü¶ x‚ÇÄ * x) ‚àò fun x ‚Ü¶ y‚ÇÄ * x) = fun x ‚Ü¶ x‚ÇÄ * y‚ÇÄ * x := by
      ext x
      simp [mul_assoc]
    calc
      map (uncurry (¬∑ * ¬∑)) (ùìù (x‚ÇÄ, y‚ÇÄ)) = map (uncurry (¬∑ * ¬∑)) (ùìù x‚ÇÄ √óÀ¢ ùìù y‚ÇÄ) := by
        rw [nhds_prod_eq]
      _ = map (fun p : M √ó M ‚Ü¶ x‚ÇÄ * p.1 * (p.2 * y‚ÇÄ)) (ùìù 1 √óÀ¢ ùìù 1) := by
        -- Porting note: `rw` was able to prove this
        -- Now it fails with `failed to rewrite using equation theorems for 'Function.uncurry'`
        -- and `failed to rewrite using equation theorems for 'Function.comp'`.
        -- Removing those two lemmas, the `rw` would succeed, but then needs a `rfl`.
        simp +unfoldPartialApp only [uncurry]
        simp_rw [hleft x‚ÇÄ, hright y‚ÇÄ, prod_map_map_eq, Filter.map_map, Function.comp_def]
      _ = map ((fun x ‚Ü¶ x‚ÇÄ * x) ‚àò fun x ‚Ü¶ x * y‚ÇÄ) (map (uncurry (¬∑ * ¬∑)) (ùìù 1 √óÀ¢ ùìù 1)) := by
        rw [key, ‚Üê Filter.map_map]
      _ ‚â§ map ((fun x : M ‚Ü¶ x‚ÇÄ * x) ‚àò fun x ‚Ü¶ x * y‚ÇÄ) (ùìù 1) := map_mono hmul
      _ = ùìù (x‚ÇÄ * y‚ÇÄ) := by
        rw [‚Üê Filter.map_map, ‚Üê hright, hleft y‚ÇÄ, Filter.map_map, key‚ÇÇ, ‚Üê hleft]‚ü©

@[to_additive]
theorem continuousMul_of_comm_of_nhds_one (M : Type u) [CommMonoid M] [TopologicalSpace M]
    (hmul : Tendsto (uncurry ((¬∑ * ¬∑) : M ‚Üí M ‚Üí M)) (ùìù 1 √óÀ¢ ùìù 1) (ùìù 1))
    (hleft : ‚àÄ x‚ÇÄ : M, ùìù x‚ÇÄ = map (fun x ‚Ü¶ x‚ÇÄ * x) (ùìù 1)) : ContinuousMul M := by
  apply ContinuousMul.of_nhds_one hmul hleft
  intro x‚ÇÄ
  simp_rw [mul_comm, hleft x‚ÇÄ]

end ContinuousMul

section PointwiseLimits

variable (M‚ÇÅ M‚ÇÇ : Type*) [TopologicalSpace M‚ÇÇ] [T2Space M‚ÇÇ]

@[to_additive]
theorem isClosed_setOf_map_one [One M‚ÇÅ] [One M‚ÇÇ] : IsClosed { f : M‚ÇÅ ‚Üí M‚ÇÇ | f 1 = 1 } :=
  isClosed_eq (continuous_apply 1) continuous_const

@[to_additive]
theorem isClosed_setOf_map_mul [Mul M‚ÇÅ] [Mul M‚ÇÇ] [ContinuousMul M‚ÇÇ] :
    IsClosed { f : M‚ÇÅ ‚Üí M‚ÇÇ | ‚àÄ x y, f (x * y) = f x * f y } := by
  simp only [setOf_forall]
  exact isClosed_iInter fun x ‚Ü¶ isClosed_iInter fun y ‚Ü¶
    isClosed_eq (continuous_apply _) (by fun_prop)

section Semigroup

variable {M‚ÇÅ M‚ÇÇ} [Mul M‚ÇÅ] [Mul M‚ÇÇ] [ContinuousMul M‚ÇÇ]
  {F : Type*} [FunLike F M‚ÇÅ M‚ÇÇ] [MulHomClass F M‚ÇÅ M‚ÇÇ] {l : Filter Œ±}

/-- Construct a bundled semigroup homomorphism `M‚ÇÅ ‚Üí‚Çô* M‚ÇÇ` from a function `f` and a proof that it
belongs to the closure of the range of the coercion from `M‚ÇÅ ‚Üí‚Çô* M‚ÇÇ` (or another type of bundled
homomorphisms that has a `MulHomClass` instance) to `M‚ÇÅ ‚Üí M‚ÇÇ`. -/
@[to_additive (attr := simps -fullyApplied)
  /-- Construct a bundled additive semigroup homomorphism `M‚ÇÅ ‚Üí‚Çô+ M‚ÇÇ` from a function `f`
and a proof that it belongs to the closure of the range of the coercion from `M‚ÇÅ ‚Üí‚Çô+ M‚ÇÇ` (or another
type of bundled homomorphisms that has an `AddHomClass` instance) to `M‚ÇÅ ‚Üí M‚ÇÇ`. -/]
def mulHomOfMemClosureRangeCoe (f : M‚ÇÅ ‚Üí M‚ÇÇ)
    (hf : f ‚àà closure (range fun (f : F) (x : M‚ÇÅ) ‚Ü¶ f x)) : M‚ÇÅ ‚Üí‚Çô* M‚ÇÇ where
  toFun := f
  map_mul' := (isClosed_setOf_map_mul M‚ÇÅ M‚ÇÇ).closure_subset_iff.2 (range_subset_iff.2 map_mul) hf

/-- Construct a bundled semigroup homomorphism from a pointwise limit of semigroup homomorphisms. -/
@[to_additive (attr := simps! -fullyApplied)
  /-- Construct a bundled additive semigroup homomorphism from a pointwise limit of additive
semigroup homomorphisms -/]
def mulHomOfTendsto (f : M‚ÇÅ ‚Üí M‚ÇÇ) (g : Œ± ‚Üí F) [l.NeBot]
    (h : Tendsto (fun a x ‚Ü¶ g a x) l (ùìù f)) : M‚ÇÅ ‚Üí‚Çô* M‚ÇÇ :=
  mulHomOfMemClosureRangeCoe f <|
    mem_closure_of_tendsto h <| Eventually.of_forall fun _ ‚Ü¶ mem_range_self _

variable (M‚ÇÅ M‚ÇÇ)

@[to_additive]
theorem MulHom.isClosed_range_coe : IsClosed (Set.range ((‚Üë) : (M‚ÇÅ ‚Üí‚Çô* M‚ÇÇ) ‚Üí M‚ÇÅ ‚Üí M‚ÇÇ)) :=
  isClosed_of_closure_subset fun f hf ‚Ü¶ ‚ü®mulHomOfMemClosureRangeCoe f hf, rfl‚ü©

end Semigroup

section Monoid

variable {M‚ÇÅ M‚ÇÇ} [MulOneClass M‚ÇÅ] [MulOneClass M‚ÇÇ] [ContinuousMul M‚ÇÇ]
  {F : Type*} [FunLike F M‚ÇÅ M‚ÇÇ] [MonoidHomClass F M‚ÇÅ M‚ÇÇ] {l : Filter Œ±}

/-- Construct a bundled monoid homomorphism `M‚ÇÅ ‚Üí* M‚ÇÇ` from a function `f` and a proof that it
belongs to the closure of the range of the coercion from `M‚ÇÅ ‚Üí* M‚ÇÇ` (or another type of bundled
homomorphisms that has a `MonoidHomClass` instance) to `M‚ÇÅ ‚Üí M‚ÇÇ`. -/
@[to_additive (attr := simps -fullyApplied)
  /-- Construct a bundled additive monoid homomorphism `M‚ÇÅ ‚Üí+ M‚ÇÇ` from a function `f`
and a proof that it belongs to the closure of the range of the coercion from `M‚ÇÅ ‚Üí+ M‚ÇÇ` (or another
type of bundled homomorphisms that has an `AddMonoidHomClass` instance) to `M‚ÇÅ ‚Üí M‚ÇÇ`. -/]
def monoidHomOfMemClosureRangeCoe (f : M‚ÇÅ ‚Üí M‚ÇÇ)
    (hf : f ‚àà closure (range fun (f : F) (x : M‚ÇÅ) ‚Ü¶ f x)) : M‚ÇÅ ‚Üí* M‚ÇÇ where
  toFun := f
  map_one' := (isClosed_setOf_map_one M‚ÇÅ M‚ÇÇ).closure_subset_iff.2 (range_subset_iff.2 map_one) hf
  map_mul' := (isClosed_setOf_map_mul M‚ÇÅ M‚ÇÇ).closure_subset_iff.2 (range_subset_iff.2 map_mul) hf

/-- Construct a bundled monoid homomorphism from a pointwise limit of monoid homomorphisms. -/
@[to_additive (attr := simps! -fullyApplied)
  /-- Construct a bundled additive monoid homomorphism from a pointwise limit of additive
monoid homomorphisms -/]
def monoidHomOfTendsto (f : M‚ÇÅ ‚Üí M‚ÇÇ) (g : Œ± ‚Üí F) [l.NeBot]
    (h : Tendsto (fun a x ‚Ü¶ g a x) l (ùìù f)) : M‚ÇÅ ‚Üí* M‚ÇÇ :=
  monoidHomOfMemClosureRangeCoe f <|
    mem_closure_of_tendsto h <| Eventually.of_forall fun _ ‚Ü¶ mem_range_self _

variable (M‚ÇÅ M‚ÇÇ)

@[to_additive]
theorem MonoidHom.isClosed_range_coe : IsClosed (Set.range ((‚Üë) : (M‚ÇÅ ‚Üí* M‚ÇÇ) ‚Üí M‚ÇÅ ‚Üí M‚ÇÇ)) :=
  isClosed_of_closure_subset fun f hf ‚Ü¶ ‚ü®monoidHomOfMemClosureRangeCoe f hf, rfl‚ü©

end Monoid

end PointwiseLimits

@[to_additive]
theorem Topology.IsInducing.continuousMul {M N F : Type*} [Mul M] [Mul N] [FunLike F M N]
    [MulHomClass F M N] [TopologicalSpace M] [TopologicalSpace N] [ContinuousMul N] (f : F)
    (hf : IsInducing f) : ContinuousMul M :=
  ‚ü®(hf.continuousSMul hf.continuous (map_mul f _ _)).1‚ü©

@[to_additive]
theorem continuousMul_induced {M N F : Type*} [Mul M] [Mul N] [FunLike F M N] [MulHomClass F M N]
    [TopologicalSpace N] [ContinuousMul N] (f : F) : @ContinuousMul M (induced f ‚Äπ_‚Ä∫) _ :=
  letI := induced f ‚Äπ_‚Ä∫
  IsInducing.continuousMul f ‚ü®rfl‚ü©

@[to_additive]
instance Subsemigroup.continuousMul [TopologicalSpace M] [Semigroup M] [ContinuousMul M]
    (S : Subsemigroup M) : ContinuousMul S :=
  IsInducing.continuousMul ({ toFun := (‚Üë), map_mul' := fun _ _ ‚Ü¶ rfl} : MulHom S M) ‚ü®rfl‚ü©

@[to_additive]
instance Submonoid.continuousMul [TopologicalSpace M] [Monoid M] [ContinuousMul M]
    (S : Submonoid M) : ContinuousMul S :=
  S.toSubsemigroup.continuousMul

section MulZeroClass

open Filter

variable {Œ± Œ≤ : Type*}
variable [TopologicalSpace M] [MulZeroClass M] [ContinuousMul M]

theorem exists_mem_nhds_zero_mul_subset
    {K U : Set M} (hK : IsCompact K) (hU : U ‚àà ùìù 0) : ‚àÉ V ‚àà ùìù 0, K * V ‚äÜ U := by
  refine hK.induction_on ?_ ?_ ?_ ?_
  ¬∑ exact ‚ü®univ, by simp‚ü©
  ¬∑ rintro s t hst ‚ü®V, hV, hV'‚ü©
    exact ‚ü®V, hV, (mul_subset_mul_right hst).trans hV'‚ü©
  ¬∑ rintro s t ‚ü®V, V_in, hV'‚ü© ‚ü®W, W_in, hW'‚ü©
    use V ‚à© W, inter_mem V_in W_in
    rw [union_mul]
    exact
      union_subset ((mul_subset_mul_left V.inter_subset_left).trans hV')
        ((mul_subset_mul_left V.inter_subset_right).trans hW')
  ¬∑ intro x hx
    have := tendsto_mul (show U ‚àà ùìù (x * 0) by simpa using hU)
    rw [nhds_prod_eq, mem_map, mem_prod_iff] at this
    rcases this with ‚ü®t, ht, s, hs, h‚ü©
    rw [‚Üê image_subset_iff, image_mul_prod] at h
    exact ‚ü®t, mem_nhdsWithin_of_mem_nhds ht, s, hs, h‚ü©

/-- Let `M` be a topological space with a continuous multiplication operation and a `0`.
Let `l` be a filter on `M` which is disjoint from the cocompact filter. Then, the multiplication map
`M √ó M ‚Üí M` tends to zero on the filter product `ùìù 0 √óÀ¢ l`. -/
theorem tendsto_mul_nhds_zero_prod_of_disjoint_cocompact {l : Filter M}
    (hl : Disjoint l (cocompact M)) :
    Tendsto (fun x : M √ó M ‚Ü¶ x.1 * x.2) (ùìù 0 √óÀ¢ l) (ùìù 0) := calc
  map (fun x : M √ó M ‚Ü¶ x.1 * x.2) (ùìù 0 √óÀ¢ l)
  _ ‚â§ map (fun x : M √ó M ‚Ü¶ x.1 * x.2) (ùìùÀ¢ ({0} √óÀ¢ Set.univ)) :=
    map_mono <| nhds_prod_le_of_disjoint_cocompact 0 hl
  _ ‚â§ ùìù 0 := continuous_mul.tendsto_nhdsSet_nhds fun _ ‚ü®hx, _‚ü© ‚Ü¶ mul_eq_zero_of_left hx _

/-- Let `M` be a topological space with a continuous multiplication operation and a `0`.
Let `l` be a filter on `M` which is disjoint from the cocompact filter. Then, the multiplication map
`M √ó M ‚Üí M` tends to zero on the filter product `l √óÀ¢ ùìù 0`. -/
theorem tendsto_mul_prod_nhds_zero_of_disjoint_cocompact {l : Filter M}
    (hl : Disjoint l (cocompact M)) :
    Tendsto (fun x : M √ó M ‚Ü¶ x.1 * x.2) (l √óÀ¢ ùìù 0) (ùìù 0) := calc
  map (fun x : M √ó M ‚Ü¶ x.1 * x.2) (l √óÀ¢ ùìù 0)
  _ ‚â§ map (fun x : M √ó M ‚Ü¶ x.1 * x.2) (ùìùÀ¢ (Set.univ √óÀ¢ {0})) :=
    map_mono <| prod_nhds_le_of_disjoint_cocompact 0 hl
  _ ‚â§ ùìù 0 := continuous_mul.tendsto_nhdsSet_nhds fun _ ‚ü®_, hx‚ü© ‚Ü¶ mul_eq_zero_of_right _ hx

/-- Let `M` be a topological space with a continuous multiplication operation and a `0`.
Let `l` be a filter on `M √ó M` which is disjoint from the cocompact filter. Then, the multiplication
map `M √ó M ‚Üí M` tends to zero on `(ùìù 0).coprod (ùìù 0) ‚äì l`. -/
theorem tendsto_mul_coprod_nhds_zero_inf_of_disjoint_cocompact {l : Filter (M √ó M)}
    (hl : Disjoint l (cocompact (M √ó M))) :
    Tendsto (fun x : M √ó M ‚Ü¶ x.1 * x.2) ((ùìù 0).coprod (ùìù 0) ‚äì l) (ùìù 0) := by
  have := calc
    (ùìù 0).coprod (ùìù 0) ‚äì l
    _ ‚â§ (ùìù 0).coprod (ùìù 0) ‚äì map Prod.fst l √óÀ¢ map Prod.snd l :=
      inf_le_inf_left _ le_prod_map_fst_snd
    _ ‚â§ ùìù 0 √óÀ¢ map Prod.snd l ‚äî map Prod.fst l √óÀ¢ ùìù 0 :=
      coprod_inf_prod_le _ _ _ _
  apply (Tendsto.sup _ _).mono_left this
  ¬∑ apply tendsto_mul_nhds_zero_prod_of_disjoint_cocompact
    exact disjoint_map_cocompact continuous_snd hl
  ¬∑ apply tendsto_mul_prod_nhds_zero_of_disjoint_cocompact
    exact disjoint_map_cocompact continuous_fst hl

/-- Let `M` be a topological space with a continuous multiplication operation and a `0`.
Let `l` be a filter on `M √ó M` which is both disjoint from the cocompact filter and less than or
equal to `(ùìù 0).coprod (ùìù 0)`. Then the multiplication map `M √ó M ‚Üí M` tends to zero on `l`. -/
theorem tendsto_mul_nhds_zero_of_disjoint_cocompact {l : Filter (M √ó M)}
    (hl : Disjoint l (cocompact (M √ó M))) (h'l : l ‚â§ (ùìù 0).coprod (ùìù 0)) :
    Tendsto (fun x : M √ó M ‚Ü¶ x.1 * x.2) l (ùìù 0) := by
  simpa [inf_eq_right.mpr h'l] using tendsto_mul_coprod_nhds_zero_inf_of_disjoint_cocompact hl

/-- Let `M` be a topological space with a continuous multiplication operation and a `0`.
Let `f : Œ± ‚Üí M` and `g : Œ± ‚Üí M` be functions. If `f` tends to zero on a filter `l`
and the image of `l` under `g` is disjoint from the cocompact filter on `M`, then
`fun x : Œ± ‚Ü¶ f x * g x` also tends to zero on `l`. -/
theorem Tendsto.tendsto_mul_zero_of_disjoint_cocompact_right {f g : Œ± ‚Üí M} {l : Filter Œ±}
    (hf : Tendsto f l (ùìù 0)) (hg : Disjoint (map g l) (cocompact M)) :
    Tendsto (fun x ‚Ü¶ f x * g x) l (ùìù 0) :=
  tendsto_mul_nhds_zero_prod_of_disjoint_cocompact hg |>.comp (hf.prodMk tendsto_map)

/-- Let `M` be a topological space with a continuous multiplication operation and a `0`.
Let `f : Œ± ‚Üí M` and `g : Œ± ‚Üí M` be functions. If `g` tends to zero on a filter `l`
and the image of `l` under `f` is disjoint from the cocompact filter on `M`, then
`fun x : Œ± ‚Ü¶ f x * g x` also tends to zero on `l`. -/
theorem Tendsto.tendsto_mul_zero_of_disjoint_cocompact_left {f g : Œ± ‚Üí M} {l : Filter Œ±}
    (hf : Disjoint (map f l) (cocompact M)) (hg : Tendsto g l (ùìù 0)) :
    Tendsto (fun x ‚Ü¶ f x * g x) l (ùìù 0) :=
  tendsto_mul_prod_nhds_zero_of_disjoint_cocompact hf |>.comp (tendsto_map.prodMk hg)

/-- If `f : Œ± ‚Üí M` and `g : Œ≤ ‚Üí M` are continuous and both tend to zero on the cocompact filter,
then `fun i : Œ± √ó Œ≤ ‚Ü¶ f i.1 * g i.2` also tends to zero on the cocompact filter. -/
theorem tendsto_mul_cocompact_nhds_zero [TopologicalSpace Œ±] [TopologicalSpace Œ≤]
    {f : Œ± ‚Üí M} {g : Œ≤ ‚Üí M} (f_cont : Continuous f) (g_cont : Continuous g)
    (hf : Tendsto f (cocompact Œ±) (ùìù 0)) (hg : Tendsto g (cocompact Œ≤) (ùìù 0)) :
    Tendsto (fun i : Œ± √ó Œ≤ ‚Ü¶ f i.1 * g i.2) (cocompact (Œ± √ó Œ≤)) (ùìù 0) := by
  set l : Filter (M √ó M) := map (Prod.map f g) (cocompact (Œ± √ó Œ≤)) with l_def
  set K : Set (M √ó M) := (insert 0 (range f)) √óÀ¢ (insert 0 (range g))
  have K_compact : IsCompact K := .prod (hf.isCompact_insert_range_of_cocompact f_cont)
    (hg.isCompact_insert_range_of_cocompact g_cont)
  have K_mem_l : K ‚àà l := eventually_map.mpr <| .of_forall fun ‚ü®x, y‚ü© ‚Ü¶
    ‚ü®mem_insert_of_mem _ (mem_range_self _), mem_insert_of_mem _ (mem_range_self _)‚ü©
  have l_compact : Disjoint l (cocompact (M √ó M)) := by
    rw [disjoint_cocompact_right]
    exact ‚ü®K, K_mem_l, K_compact‚ü©
  have l_le_coprod : l ‚â§ (ùìù 0).coprod (ùìù 0) := by
    rw [l_def, ‚Üê coprod_cocompact]
    exact hf.prodMap_coprod hg
  exact tendsto_mul_nhds_zero_of_disjoint_cocompact l_compact l_le_coprod |>.comp tendsto_map

/-- If `f : Œ± ‚Üí M` and `g : Œ≤ ‚Üí M` both tend to zero on the cofinite filter, then so does
`fun i : Œ± √ó Œ≤ ‚Ü¶ f i.1 * g i.2`. -/
theorem tendsto_mul_cofinite_nhds_zero {f : Œ± ‚Üí M} {g : Œ≤ ‚Üí M}
    (hf : Tendsto f cofinite (ùìù 0)) (hg : Tendsto g cofinite (ùìù 0)) :
    Tendsto (fun i : Œ± √ó Œ≤ ‚Ü¶ f i.1 * g i.2) cofinite (ùìù 0) := by
  letI : TopologicalSpace Œ± := ‚ä•
  haveI : DiscreteTopology Œ± := discreteTopology_bot Œ±
  letI : TopologicalSpace Œ≤ := ‚ä•
  haveI : DiscreteTopology Œ≤ := discreteTopology_bot Œ≤
  rw [‚Üê cocompact_eq_cofinite] at *
  exact tendsto_mul_cocompact_nhds_zero
    continuous_of_discreteTopology continuous_of_discreteTopology hf hg

end MulZeroClass

section GroupWithZero

lemma GroupWithZero.isOpen_singleton_zero [GroupWithZero M] [TopologicalSpace M]
    [ContinuousMul M] [CompactSpace M] [T1Space M] :
    IsOpen {(0 : M)} := by
  obtain ‚ü®U, hU, h0U, h1U‚ü© := t1Space_iff_exists_open.mp ‚Äπ_‚Ä∫ zero_ne_one
  obtain ‚ü®W, hW, hW'‚ü© := exists_mem_nhds_zero_mul_subset isCompact_univ (hU.mem_nhds h0U)
  by_cases H : ‚àÉ x ‚â† 0, x ‚àà W
  ¬∑ obtain ‚ü®x, hx, hxW‚ü© := H
    cases h1U (hW' (by simpa [hx] using Set.mul_mem_mul (Set.mem_univ x‚Åª¬π) hxW))
  ¬∑ obtain rfl : W = {0} := subset_antisymm
      (by simpa [not_imp_not] using H) (by simpa using mem_of_mem_nhds hW)
    simpa [isOpen_iff_mem_nhds]

end GroupWithZero

section MulOneClass

variable [TopologicalSpace M] [MulOneClass M] [ContinuousMul M]

@[to_additive exists_open_nhds_zero_half]
theorem exists_open_nhds_one_split {s : Set M} (hs : s ‚àà ùìù (1 : M)) :
    ‚àÉ V : Set M, IsOpen V ‚àß (1 : M) ‚àà V ‚àß ‚àÄ v ‚àà V, ‚àÄ w ‚àà V, v * w ‚àà s := by
  have : (fun a : M √ó M ‚Ü¶ a.1 * a.2) ‚Åª¬π' s ‚àà ùìù ((1, 1) : M √ó M) :=
    tendsto_mul (by simpa only [one_mul] using hs)
  simpa only [prod_subset_iff] using exists_nhds_square this

@[to_additive exists_nhds_zero_half]
theorem exists_nhds_one_split {s : Set M} (hs : s ‚àà ùìù (1 : M)) :
    ‚àÉ V ‚àà ùìù (1 : M), ‚àÄ v ‚àà V, ‚àÄ w ‚àà V, v * w ‚àà s :=
  let ‚ü®V, Vo, V1, hV‚ü© := exists_open_nhds_one_split hs
  ‚ü®V, IsOpen.mem_nhds Vo V1, hV‚ü©

/-- Given a neighborhood `U` of `1` there is an open neighborhood `V` of `1`
such that `V * V ‚äÜ U`. -/
@[to_additive /-- Given an open neighborhood `U` of `0` there is an open neighborhood `V` of `0`
  such that `V + V ‚äÜ U`. -/]
theorem exists_open_nhds_one_mul_subset {U : Set M} (hU : U ‚àà ùìù (1 : M)) :
    ‚àÉ V : Set M, IsOpen V ‚àß (1 : M) ‚àà V ‚àß V * V ‚äÜ U := by
  simpa only [mul_subset_iff] using exists_open_nhds_one_split hU

@[to_additive]
theorem Filter.HasBasis.mul_self {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí Set M} (h : (ùìù 1).HasBasis p s) :
    (ùìù 1).HasBasis p fun i ‚Ü¶ s i * s i := by
  rw [‚Üê nhds_mul_nhds_one, ‚Üê map‚ÇÇ_mul, ‚Üê map_uncurry_prod]
  simpa only [‚Üê image_mul_prod] using h.prod_self.map _

end MulOneClass

section ContinuousMul

section Semigroup

variable [TopologicalSpace M] [Semigroup M] [ContinuousMul M]

@[to_additive]
theorem Subsemigroup.top_closure_mul_self_subset (s : Subsemigroup M) :
    _root_.closure (s : Set M) * _root_.closure s ‚äÜ _root_.closure s :=
  image2_subset_iff.2 fun _ hx _ hy ‚Ü¶
    map_mem_closure‚ÇÇ continuous_mul hx hy fun _ ha _ hb ‚Ü¶ s.mul_mem ha hb

/-- The (topological-space) closure of a subsemigroup of a space `M` with `ContinuousMul` is
itself a subsemigroup. -/
@[to_additive /-- The (topological-space) closure of an additive submonoid of a space `M` with
`ContinuousAdd` is itself an additive submonoid. -/]
def Subsemigroup.topologicalClosure (s : Subsemigroup M) : Subsemigroup M where
  carrier := _root_.closure (s : Set M)
  mul_mem' ha hb := s.top_closure_mul_self_subset ‚ü®_, ha, _, hb, rfl‚ü©

@[to_additive]
theorem Subsemigroup.coe_topologicalClosure (s : Subsemigroup M) :
    (s.topologicalClosure : Set M) = _root_.closure (s : Set M) := rfl

@[to_additive]
theorem Subsemigroup.le_topologicalClosure (s : Subsemigroup M) : s ‚â§ s.topologicalClosure :=
  _root_.subset_closure

@[to_additive]
theorem Subsemigroup.isClosed_topologicalClosure (s : Subsemigroup M) :
    IsClosed (s.topologicalClosure : Set M) := isClosed_closure

@[to_additive]
theorem Subsemigroup.topologicalClosure_minimal (s : Subsemigroup M) {t : Subsemigroup M}
    (h : s ‚â§ t) (ht : IsClosed (t : Set M)) : s.topologicalClosure ‚â§ t := closure_minimal h ht

/-- If a subsemigroup of a topological semigroup is commutative, then so is its topological
closure.

See note [reducible non-instances] -/
@[to_additive /-- If a submonoid of an additive topological monoid is commutative, then so is its
topological closure.

See note [reducible non-instances] -/]
abbrev Subsemigroup.commSemigroupTopologicalClosure [T2Space M] (s : Subsemigroup M)
    (hs : ‚àÄ x y : s, x * y = y * x) : CommSemigroup s.topologicalClosure :=
  { MulMemClass.toSemigroup s.topologicalClosure with
    mul_comm :=
      have : ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, x * y = y * x := fun x hx y hy ‚Ü¶
        congr_arg Subtype.val (hs ‚ü®x, hx‚ü© ‚ü®y, hy‚ü©)
      fun ‚ü®x, hx‚ü© ‚ü®y, hy‚ü© ‚Ü¶
      Subtype.ext <|
        eqOn_closure‚ÇÇ this continuous_mul (continuous_snd.mul continuous_fst) x hx y hy }

@[to_additive]
theorem IsCompact.mul {s t : Set M} (hs : IsCompact s) (ht : IsCompact t) : IsCompact (s * t) := by
  rw [‚Üê image_mul_prod]
  exact (hs.prod ht).image continuous_mul

end Semigroup

variable [TopologicalSpace M] [Monoid M] [ContinuousMul M]

@[to_additive]
theorem Submonoid.top_closure_mul_self_subset (s : Submonoid M) :
    _root_.closure (s : Set M) * _root_.closure s ‚äÜ _root_.closure s :=
  image2_subset_iff.2 fun _ hx _ hy ‚Ü¶
    map_mem_closure‚ÇÇ continuous_mul hx hy fun _ ha _ hb ‚Ü¶ s.mul_mem ha hb

@[to_additive]
theorem Submonoid.top_closure_mul_self_eq (s : Submonoid M) :
    _root_.closure (s : Set M) * _root_.closure s = _root_.closure s :=
  Subset.antisymm s.top_closure_mul_self_subset fun x hx ‚Ü¶
    ‚ü®x, hx, 1, _root_.subset_closure s.one_mem, mul_one _‚ü©

/-- The (topological-space) closure of a submonoid of a space `M` with `ContinuousMul` is
itself a submonoid. -/
@[to_additive /-- The (topological-space) closure of an additive submonoid of a space `M` with
`ContinuousAdd` is itself an additive submonoid. -/]
def Submonoid.topologicalClosure (s : Submonoid M) : Submonoid M where
  carrier := _root_.closure (s : Set M)
  one_mem' := _root_.subset_closure s.one_mem
  mul_mem' ha hb := s.top_closure_mul_self_subset ‚ü®_, ha, _, hb, rfl‚ü©

@[to_additive]
theorem Submonoid.coe_topologicalClosure (s : Submonoid M) :
    (s.topologicalClosure : Set M) = _root_.closure (s : Set M) := rfl

@[to_additive]
theorem Submonoid.le_topologicalClosure (s : Submonoid M) : s ‚â§ s.topologicalClosure :=
  _root_.subset_closure

@[to_additive]
theorem Submonoid.isClosed_topologicalClosure (s : Submonoid M) :
    IsClosed (s.topologicalClosure : Set M) := isClosed_closure

@[to_additive]
theorem Submonoid.topologicalClosure_minimal (s : Submonoid M) {t : Submonoid M} (h : s ‚â§ t)
    (ht : IsClosed (t : Set M)) : s.topologicalClosure ‚â§ t := closure_minimal h ht

/-- If a submonoid of a topological monoid is commutative, then so is its topological closure. -/
@[to_additive /-- If a submonoid of an additive topological monoid is commutative, then so is its
topological closure.

See note [reducible non-instances]. -/]
abbrev Submonoid.commMonoidTopologicalClosure [T2Space M] (s : Submonoid M)
    (hs : ‚àÄ x y : s, x * y = y * x) : CommMonoid s.topologicalClosure :=
  { s.topologicalClosure.toMonoid, s.toSubsemigroup.commSemigroupTopologicalClosure hs with }

@[to_additive exists_nhds_zero_quarter]
theorem exists_nhds_one_split4 {u : Set M} (hu : u ‚àà ùìù (1 : M)) :
    ‚àÉ V ‚àà ùìù (1 : M), ‚àÄ {v w s t}, v ‚àà V ‚Üí w ‚àà V ‚Üí s ‚àà V ‚Üí t ‚àà V ‚Üí v * w * s * t ‚àà u := by
  rcases exists_nhds_one_split hu with ‚ü®W, W1, h‚ü©
  rcases exists_nhds_one_split W1 with ‚ü®V, V1, h'‚ü©
  use V, V1
  intro v w s t v_in w_in s_in t_in
  simpa only [mul_assoc] using h _ (h' v v_in w w_in) _ (h' s s_in t t_in)

@[to_additive]
theorem tendsto_list_prod {f : Œπ ‚Üí Œ± ‚Üí M} {x : Filter Œ±} {a : Œπ ‚Üí M} :
    ‚àÄ l : List Œπ,
      (‚àÄ i ‚àà l, Tendsto (f i) x (ùìù (a i))) ‚Üí
        Tendsto (fun b ‚Ü¶ (l.map fun c ‚Ü¶ f c b).prod) x (ùìù (l.map a).prod)
  | [], _ => by simp [tendsto_const_nhds]
  | f::l, h => by
    simp only [List.map_cons, List.prod_cons]
    exact
      (h f List.mem_cons_self).mul
        (tendsto_list_prod l fun c hc ‚Ü¶ h c (List.mem_cons_of_mem _ hc))

@[to_additive (attr := continuity)]
theorem continuous_list_prod {f : Œπ ‚Üí X ‚Üí M} (l : List Œπ) (h : ‚àÄ i ‚àà l, Continuous (f i)) :
    Continuous fun a ‚Ü¶ (l.map fun i ‚Ü¶ f i a).prod :=
  continuous_iff_continuousAt.2 fun x ‚Ü¶
    tendsto_list_prod l fun c hc ‚Ü¶ continuous_iff_continuousAt.1 (h c hc) x

@[to_additive]
theorem continuousOn_list_prod {f : Œπ ‚Üí X ‚Üí M} (l : List Œπ) {t : Set X}
    (h : ‚àÄ i ‚àà l, ContinuousOn (f i) t) :
    ContinuousOn (fun a ‚Ü¶ (l.map fun i ‚Ü¶ f i a).prod) t := by
  intro x hx
  rw [continuousWithinAt_iff_continuousAt_restrict _ hx]
  refine tendsto_list_prod _ fun i hi ‚Ü¶ ?_
  specialize h i hi x hx
  rw [continuousWithinAt_iff_continuousAt_restrict _ hx] at h
  exact h

@[to_additive (attr := continuity)]
theorem continuous_pow : ‚àÄ n : ‚Ñï, Continuous fun a : M ‚Ü¶ a ^ n
  | 0 => by simpa using continuous_const
  | k + 1 => by
    simp only [pow_succ']
    exact continuous_id.mul (continuous_pow _)

instance AddMonoid.continuousConstSMul_nat {A} [AddMonoid A] [TopologicalSpace A]
    [ContinuousAdd A] : ContinuousConstSMul ‚Ñï A :=
  ‚ü®continuous_nsmul‚ü©

instance AddMonoid.continuousSMul_nat {A} [AddMonoid A] [TopologicalSpace A]
    [ContinuousAdd A] : ContinuousSMul ‚Ñï A :=
  ‚ü®continuous_prod_of_discrete_left.mpr continuous_nsmul‚ü©

-- We register `Continuous.pow` as a `continuity` lemma with low penalty (so
-- `continuity` will try it before other `continuity` lemmas). This is a
-- workaround for goals of the form `Continuous fun x ‚Ü¶ x ^ 2`, where
-- `continuity` applies `Continuous.mul` since the goal is defeq to
-- `Continuous fun x ‚Ü¶ x * x`.
--
-- To properly fix this, we should make sure that `continuity` applies its
-- lemmas with reducible transparency, preventing the unfolding of `^`. But this
-- is quite an invasive change.
@[to_additive (attr := aesop safe -100 (rule_sets := [Continuous]), fun_prop)]
theorem Continuous.pow {f : X ‚Üí M} (h : Continuous f) (n : ‚Ñï) : Continuous fun b ‚Ü¶ f b ^ n :=
  (continuous_pow n).comp h

@[to_additive]
theorem continuousOn_pow {s : Set M} (n : ‚Ñï) : ContinuousOn (fun (x : M) ‚Ü¶ x ^ n) s :=
  (continuous_pow n).continuousOn

@[to_additive]
theorem continuousAt_pow (x : M) (n : ‚Ñï) : ContinuousAt (fun (x : M) ‚Ü¶ x ^ n) x :=
  (continuous_pow n).continuousAt

@[to_additive]
theorem Filter.Tendsto.pow {l : Filter Œ±} {f : Œ± ‚Üí M} {x : M} (hf : Tendsto f l (ùìù x)) (n : ‚Ñï) :
    Tendsto (fun x ‚Ü¶ f x ^ n) l (ùìù (x ^ n)) :=
  (continuousAt_pow _ _).tendsto.comp hf

@[to_additive]
theorem ContinuousWithinAt.pow {f : X ‚Üí M} {x : X} {s : Set X} (hf : ContinuousWithinAt f s x)
    (n : ‚Ñï) : ContinuousWithinAt (fun x ‚Ü¶ f x ^ n) s x :=
  Filter.Tendsto.pow hf n

@[to_additive (attr := fun_prop)]
theorem ContinuousAt.pow {f : X ‚Üí M} {x : X} (hf : ContinuousAt f x) (n : ‚Ñï) :
    ContinuousAt (fun x ‚Ü¶ f x ^ n) x :=
  Filter.Tendsto.pow hf n

@[to_additive (attr := fun_prop)]
theorem ContinuousOn.pow {f : X ‚Üí M} {s : Set X} (hf : ContinuousOn f s) (n : ‚Ñï) :
    ContinuousOn (fun x ‚Ü¶ f x ^ n) s := fun x hx ‚Ü¶ (hf x hx).pow n

/-- Left-multiplication by a left-invertible element of a topological monoid is proper, i.e.,
inverse images of compact sets are compact. -/
theorem Filter.tendsto_cocompact_mul_left {a b : M} (ha : b * a = 1) :
    Filter.Tendsto (fun x : M ‚Ü¶ a * x) (Filter.cocompact M) (Filter.cocompact M) := by
  refine Filter.Tendsto.of_tendsto_comp ?_ (Filter.comap_cocompact_le (continuous_mul_left b))
  convert Filter.tendsto_id
  ext x
  simp [‚Üê mul_assoc, ha]

/-- Right-multiplication by a right-invertible element of a topological monoid is proper, i.e.,
inverse images of compact sets are compact. -/
theorem Filter.tendsto_cocompact_mul_right {a b : M} (ha : a * b = 1) :
    Filter.Tendsto (fun x : M ‚Ü¶ x * a) (Filter.cocompact M) (Filter.cocompact M) := by
  refine Filter.Tendsto.of_tendsto_comp ?_ (Filter.comap_cocompact_le (continuous_mul_right b))
  simp only [comp_mul_right, ha, mul_one]
  exact Filter.tendsto_id

/-- If `R` acts on `A` via `A`, then continuous multiplication implies continuous scalar
multiplication by constants.

Notably, this instances applies when `R = A`, or when `[Algebra R A]` is available. -/
@[to_additive /-- If `R` acts on `A` via `A`, then continuous addition implies
continuous affine addition by constants. -/]
instance (priority := 100) IsScalarTower.continuousConstSMul {R A : Type*} [Monoid A] [SMul R A]
    [IsScalarTower R A A] [TopologicalSpace A] [ContinuousMul A] : ContinuousConstSMul R A where
  continuous_const_smul q := by
    simp +singlePass only [‚Üê smul_one_mul q (_ : A)]
    exact continuous_const.mul continuous_id

/-- If the action of `R` on `A` commutes with left-multiplication, then continuous multiplication
implies continuous scalar multiplication by constants.

Notably, this instances applies when `R = A·µê·µí·µñ`. -/
@[to_additive /-- If the action of `R` on `A` commutes with left-addition, then
continuous addition implies continuous affine addition by constants.

Notably, this instances applies when `R = A·µÉ·µí·µñ`. -/]
instance (priority := 100) SMulCommClass.continuousConstSMul {R A : Type*} [Monoid A] [SMul R A]
    [SMulCommClass R A A] [TopologicalSpace A] [ContinuousMul A] : ContinuousConstSMul R A where
  continuous_const_smul q := by
    simp +singlePass only [‚Üê mul_smul_one q (_ : A)]
    exact continuous_id.mul continuous_const

end ContinuousMul

namespace MulOpposite

/-- If multiplication is continuous in `Œ±`, then it also is in `Œ±·µê·µí·µñ`. -/
@[to_additive /-- If addition is continuous in `Œ±`, then it also is in `Œ±·µÉ·µí·µñ`. -/]
instance [TopologicalSpace Œ±] [Mul Œ±] [ContinuousMul Œ±] : ContinuousMul Œ±·µê·µí·µñ :=
  ‚ü®continuous_op.comp (continuous_unop.snd'.mul continuous_unop.fst')‚ü©

end MulOpposite

namespace Units

open MulOpposite

variable [TopologicalSpace Œ±] [Monoid Œ±] [ContinuousMul Œ±]

/-- If multiplication on a monoid is continuous, then multiplication on the units of the monoid,
with respect to the induced topology, is continuous.

Inversion is also continuous, but we register this in a later file, `Topology.Algebra.Group`,
because the predicate `ContinuousInv` has not yet been defined. -/
@[to_additive /-- If addition on an additive monoid is continuous, then addition on the additive
units of the monoid, with respect to the induced topology, is continuous.

Negation is also continuous, but we register this in a later file, `Topology.Algebra.Group`, because
the predicate `ContinuousNeg` has not yet been defined. -/]
instance : ContinuousMul Œ±À£ := isInducing_embedProduct.continuousMul (embedProduct Œ±)

end Units

@[to_additive (attr := fun_prop)]
theorem Continuous.units_map [Monoid M] [Monoid N] [TopologicalSpace M] [TopologicalSpace N]
    (f : M ‚Üí* N) (hf : Continuous f) : Continuous (Units.map f) :=
  Units.continuous_iff.2 ‚ü®hf.comp Units.continuous_val, hf.comp Units.continuous_coe_inv‚ü©

section

variable [TopologicalSpace M] [CommMonoid M]

@[to_additive]
theorem Submonoid.mem_nhds_one (S : Submonoid M) (oS : IsOpen (S : Set M)) :
    (S : Set M) ‚àà ùìù (1 : M) :=
  IsOpen.mem_nhds oS S.one_mem

variable [ContinuousMul M]

@[to_additive]
theorem tendsto_multiset_prod {f : Œπ ‚Üí Œ± ‚Üí M} {x : Filter Œ±} {a : Œπ ‚Üí M} (s : Multiset Œπ) :
    (‚àÄ i ‚àà s, Tendsto (f i) x (ùìù (a i))) ‚Üí
      Tendsto (fun b ‚Ü¶ (s.map fun c ‚Ü¶ f c b).prod) x (ùìù (s.map a).prod) := by
  rcases s with ‚ü®l‚ü©
  simpa using tendsto_list_prod l

@[to_additive]
theorem tendsto_finset_prod {f : Œπ ‚Üí Œ± ‚Üí M} {x : Filter Œ±} {a : Œπ ‚Üí M} (s : Finset Œπ) :
    (‚àÄ i ‚àà s, Tendsto (f i) x (ùìù (a i))) ‚Üí
      Tendsto (fun b ‚Ü¶ ‚àè c ‚àà s, f c b) x (ùìù (‚àè c ‚àà s, a c)) :=
  tendsto_multiset_prod _

@[to_additive (attr := continuity)]
theorem continuous_multiset_prod {f : Œπ ‚Üí X ‚Üí M} (s : Multiset Œπ) :
    (‚àÄ i ‚àà s, Continuous (f i)) ‚Üí Continuous fun a ‚Ü¶ (s.map fun i ‚Ü¶ f i a).prod := by
  rcases s with ‚ü®l‚ü©
  simpa using continuous_list_prod l

@[to_additive]
theorem continuousOn_multiset_prod {f : Œπ ‚Üí X ‚Üí M} (s : Multiset Œπ) {t : Set X} :
    (‚àÄ i ‚àà s, ContinuousOn (f i) t) ‚Üí ContinuousOn (fun a ‚Ü¶ (s.map fun i ‚Ü¶ f i a).prod) t := by
  rcases s with ‚ü®l‚ü©
  simpa using continuousOn_list_prod l

@[to_additive (attr := continuity, fun_prop)]
theorem continuous_finset_prod {f : Œπ ‚Üí X ‚Üí M} (s : Finset Œπ) :
    (‚àÄ i ‚àà s, Continuous (f i)) ‚Üí Continuous fun a ‚Ü¶ ‚àè i ‚àà s, f i a :=
  continuous_multiset_prod _

@[to_additive]
theorem continuousOn_finset_prod {f : Œπ ‚Üí X ‚Üí M} (s : Finset Œπ) {t : Set X} :
    (‚àÄ i ‚àà s, ContinuousOn (f i) t) ‚Üí ContinuousOn (fun a ‚Ü¶ ‚àè i ‚àà s, f i a) t :=
  continuousOn_multiset_prod _

@[to_additive]
theorem eventuallyEq_prod {X M : Type*} [CommMonoid M] {s : Finset Œπ} {l : Filter X}
    {f g : Œπ ‚Üí X ‚Üí M} (hs : ‚àÄ i ‚àà s, f i =·∂†[l] g i) : ‚àè i ‚àà s, f i =·∂†[l] ‚àè i ‚àà s, g i := by
  replace hs : ‚àÄ·∂† x in l, ‚àÄ i ‚àà s, f i x = g i x := by rwa [eventually_all_finset]
  filter_upwards [hs] with x hx
  simp only [Finset.prod_apply, Finset.prod_congr rfl hx]

open Function

@[to_additive]
theorem LocallyFinite.exists_finset_mulSupport {M : Type*} [One M] {f : Œπ ‚Üí X ‚Üí M}
    (hf : LocallyFinite fun i ‚Ü¶ mulSupport <| f i) (x‚ÇÄ : X) :
    ‚àÉ I : Finset Œπ, ‚àÄ·∂† x in ùìù x‚ÇÄ, (mulSupport fun i ‚Ü¶ f i x) ‚äÜ I := by
  rcases hf x‚ÇÄ with ‚ü®U, hxU, hUf‚ü©
  refine ‚ü®hUf.toFinset, mem_of_superset hxU fun y hy i hi ‚Ü¶ ?_‚ü©
  rw [hUf.coe_toFinset]
  exact ‚ü®y, hi, hy‚ü©

@[to_additive]
theorem finprod_eventually_eq_prod {M : Type*} [CommMonoid M] {f : Œπ ‚Üí X ‚Üí M}
    (hf : LocallyFinite fun i ‚Ü¶ mulSupport (f i)) (x : X) :
    ‚àÉ s : Finset Œπ, ‚àÄ·∂† y in ùìù x, ‚àè·∂† i, f i y = ‚àè i ‚àà s, f i y :=
  let ‚ü®I, hI‚ü© := hf.exists_finset_mulSupport x
  ‚ü®I, hI.mono fun _ hy ‚Ü¶ finprod_eq_prod_of_mulSupport_subset _ fun _ hi ‚Ü¶ hy hi‚ü©

@[to_additive]
theorem continuous_finprod {f : Œπ ‚Üí X ‚Üí M} (hc : ‚àÄ i, Continuous (f i))
    (hf : LocallyFinite fun i ‚Ü¶ mulSupport (f i)) : Continuous fun x ‚Ü¶ ‚àè·∂† i, f i x := by
  refine continuous_iff_continuousAt.2 fun x ‚Ü¶ ?_
  rcases finprod_eventually_eq_prod hf x with ‚ü®s, hs‚ü©
  refine ContinuousAt.congr ?_ (EventuallyEq.symm hs)
  exact tendsto_finset_prod _ fun i _ ‚Ü¶ (hc i).continuousAt

@[to_additive]
theorem continuous_finprod_cond {f : Œπ ‚Üí X ‚Üí M} {p : Œπ ‚Üí Prop} (hc : ‚àÄ i, p i ‚Üí Continuous (f i))
    (hf : LocallyFinite fun i ‚Ü¶ mulSupport (f i)) :
    Continuous fun x ‚Ü¶ ‚àè·∂† (i) (_ : p i), f i x := by
  simp only [‚Üê finprod_subtype_eq_finprod_cond]
  exact continuous_finprod (fun i ‚Ü¶ hc i i.2) (hf.comp_injective Subtype.coe_injective)

end

instance [TopologicalSpace M] [Mul M] [ContinuousMul M] : ContinuousAdd (Additive M) where
  continuous_add := @continuous_mul M _ _ _

instance [TopologicalSpace M] [Add M] [ContinuousAdd M] : ContinuousMul (Multiplicative M) where
  continuous_mul := @continuous_add M _ _ _

section LatticeOps

variable {Œπ' : Sort*} [Mul M]

@[to_additive]
theorem continuousMul_sInf {ts : Set (TopologicalSpace M)}
    (h : ‚àÄ t ‚àà ts, @ContinuousMul M t _) : @ContinuousMul M (sInf ts) _ :=
  letI := sInf ts
  { continuous_mul :=
      continuous_sInf_rng.2 fun t ht ‚Ü¶
        continuous_sInf_dom‚ÇÇ ht ht (@ContinuousMul.continuous_mul M t _ (h t ht)) }

@[to_additive]
theorem continuousMul_iInf {ts : Œπ' ‚Üí TopologicalSpace M}
    (h' : ‚àÄ i, @ContinuousMul M (ts i) _) : @ContinuousMul M (‚®Ö i, ts i) _ := by
  rw [‚Üê sInf_range]
  exact continuousMul_sInf (Set.forall_mem_range.mpr h')

@[to_additive]
theorem continuousMul_inf {t‚ÇÅ t‚ÇÇ : TopologicalSpace M} (h‚ÇÅ : @ContinuousMul M t‚ÇÅ _)
    (h‚ÇÇ : @ContinuousMul M t‚ÇÇ _) : @ContinuousMul M (t‚ÇÅ ‚äì t‚ÇÇ) _ := by
  rw [inf_eq_iInf]
  refine continuousMul_iInf fun b ‚Ü¶ ?_
  cases b <;> assumption

end LatticeOps

namespace ContinuousMap

variable [Mul X] [ContinuousMul X]

/-- The continuous map `fun y ‚Ü¶ y * x` -/
@[to_additive /-- The continuous map `fun y ‚Ü¶ y + x` -/]
protected def mulRight (x : X) : C(X, X) :=
  mk _ (continuous_mul_right x)

@[to_additive (attr := simp)]
theorem coe_mulRight (x : X) : ‚áë(ContinuousMap.mulRight x) = fun y ‚Ü¶ y * x :=
  rfl

/-- The continuous map `fun y ‚Ü¶ x * y` -/
@[to_additive /-- The continuous map `fun y ‚Ü¶ x + y` -/]
protected def mulLeft (x : X) : C(X, X) :=
  mk _ (continuous_mul_left x)

@[to_additive (attr := simp)]
theorem coe_mulLeft (x : X) : ‚áë(ContinuousMap.mulLeft x) = fun y ‚Ü¶ x * y :=
  rfl

end ContinuousMap
