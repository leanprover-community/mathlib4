/-
Copyright (c) 2024 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Nailin Guan, Yuyang Zhao
-/
import Mathlib.Algebra.Category.Grp.FiniteGrp
import Mathlib.Topology.Algebra.ClosedSubgroup
import Mathlib.Topology.Algebra.ContinuousMonoidHom
import Mathlib.Topology.Category.Profinite.Basic
/-!

# Category of Profinite Groups

We say `G` is a profinite group if it is a topological group which is compact and totally
disconnected.

## Main definitions and results

* `ProfiniteGrp` is the category of profinite groups.

* `ProfiniteGrp.pi` : The pi-type of profinite groups is also a profinite group.

* `ofFiniteGrp` : A `FiniteGrp` when given the discrete topology can be considered as a
  profinite group.

* `ofClosedSubgroup` : A closed subgroup of a profinite group is profinite.

-/

universe u v

open CategoryTheory Topology

/--
The category of profinite groups. A term of this type consists of a profinite
set with a topological group structure.
-/
@[pp_with_univ]
structure ProfiniteGrp where
  /-- The underlying profinite topological space. -/
  toProfinite : Profinite
  /-- The group structure. -/
  [group : Group toProfinite]
  /-- The above data together form a topological group. -/
  [topologicalGroup : TopologicalGroup toProfinite]

/--
The category of profinite additive groups. A term of this type consists of a profinite
set with a topological additive group structure.
-/
@[pp_with_univ]
structure ProfiniteAddGrp where
  /-- The underlying profinite topological space. -/
  toProfinite : Profinite
  /-- The additive group structure. -/
  [addGroup : AddGroup toProfinite]
  /-- The above data together form a topological additive group. -/
  [topologicalAddGroup : TopologicalAddGroup toProfinite]

attribute [to_additive] ProfiniteGrp

namespace ProfiniteGrp

@[to_additive]
instance : CoeSort ProfiniteGrp (Type u) where
  coe G := G.toProfinite

attribute [instance] group topologicalGroup
    ProfiniteAddGrp.addGroup ProfiniteAddGrp.topologicalAddGroup

@[to_additive]
instance : Category ProfiniteGrp where
  Hom A B := ContinuousMonoidHom A B
  id A := ContinuousMonoidHom.id A
  comp f g := ContinuousMonoidHom.comp g f

@[to_additive]
instance (G H : ProfiniteGrp) : FunLike (G âŸ¶ H) G H :=
  inferInstanceAs <| FunLike (ContinuousMonoidHom G H) G H

@[to_additive]
instance (G H : ProfiniteGrp) : MonoidHomClass (G âŸ¶ H) G H :=
  inferInstanceAs <| MonoidHomClass (ContinuousMonoidHom G H) G H

@[to_additive]
instance (G H : ProfiniteGrp) : ContinuousMapClass (G âŸ¶ H) G H :=
  inferInstanceAs <| ContinuousMapClass (ContinuousMonoidHom G H) G H

@[to_additive]
instance : ConcreteCategory ProfiniteGrp where
  forget :=
  { obj := fun G => G
    map := fun f => f }
  forget_faithful :=
    { map_injective := by
        intro G H f g h
        exact DFunLike.ext _ _ <| fun x => congr_fun h x }

/-- Construct a term of `ProfiniteGrp` from a type endowed with the structure of a
compact and totally disconnected topological group.
(The condition of being Hausdorff can be omitted here because totally disconnected implies that {1}
is a closed set, thus implying Hausdorff in a topological group.)-/
@[to_additive "Construct a term of `ProfiniteAddGrp` from a type endowed with the structure of a
compact and totally disconnected topological additive group.
(The condition of being Hausdorff can be omitted here because totally disconnected implies that {0}
is a closed set, thus implying Hausdorff in a topological additive group.)"]
def of (G : Type u) [Group G] [TopologicalSpace G] [TopologicalGroup G]
    [CompactSpace G] [TotallyDisconnectedSpace G] : ProfiniteGrp where
  toProfinite := .of G
  group := â€¹_â€º
  topologicalGroup := â€¹_â€º

@[to_additive (attr := simp)]
theorem coe_of (X : ProfiniteGrp) : (of X : Type _) = X :=
  rfl

@[to_additive (attr := simp)]
theorem coe_id (X : ProfiniteGrp) : (ðŸ™ ((forget ProfiniteGrp).obj X)) = id :=
  rfl

@[to_additive (attr := simp)]
theorem coe_comp {X Y Z : ProfiniteGrp} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    ((forget ProfiniteGrp).map f â‰« (forget ProfiniteGrp).map g) = g âˆ˜ f :=
  rfl

/-- Construct a term of `ProfiniteGrp` from a type endowed with the structure of a
profinite topological group. -/
@[to_additive "Construct a term of `ProfiniteAddGrp` from a type endowed with the structure of a
profinite topological additive group."]
abbrev ofProfinite (G : Profinite) [Group G] [TopologicalGroup G] :
    ProfiniteGrp := of G

/-- The pi-type of profinite groups is a profinite group. -/
@[to_additive "The pi-type of profinite additive groups is a
profinite additive group."]
def pi {Î± : Type u} (Î² : Î± â†’ ProfiniteGrp) : ProfiniteGrp :=
  let pitype := Profinite.pi fun (a : Î±) => (Î² a).toProfinite
  letI (a : Î±): Group (Î² a).toProfinite := (Î² a).group
  letI : Group pitype := Pi.group
  letI : TopologicalGroup pitype := Pi.topologicalGroup
  ofProfinite pitype

/-- A `FiniteGrp` when given the discrete topology can be considered as a profinite group. -/
@[to_additive "A `FiniteAddGrp` when given the discrete topology can be considered as a
profinite additive group."]
def ofFiniteGrp (G : FiniteGrp) : ProfiniteGrp :=
  letI : TopologicalSpace G := âŠ¥
  letI : DiscreteTopology G := âŸ¨rflâŸ©
  letI : TopologicalGroup G := {}
  of G

@[to_additive]
instance : HasForgetâ‚‚ FiniteGrp ProfiniteGrp where
  forgetâ‚‚ :=
  { obj := ofFiniteGrp
    map := fun f => âŸ¨f, by continuityâŸ© }

@[to_additive]
instance : HasForgetâ‚‚ ProfiniteGrp Grp where
  forgetâ‚‚ := {
    obj := fun P => âŸ¨P, P.groupâŸ©
    map := fun f => f.toMonoidHom
  }

/-- A closed subgroup of a profinite group is profinite. -/
def ofClosedSubgroup {G : ProfiniteGrp} (H : ClosedSubgroup G)  : ProfiniteGrp :=
  letI : CompactSpace H := inferInstance
  of H.1

/-- The functor mapping a profinite group to its underlying profinite space. -/
def profiniteGrpToProfinite : ProfiniteGrp â¥¤ Profinite where
  obj G := G.toProfinite
  map f := âŸ¨f, by continuityâŸ©

instance : profiniteGrpToProfinite.Faithful := {
  map_injective := fun {_ _} _ _ h =>
    ConcreteCategory.hom_ext_iff.mpr (congrFun (congrArg ContinuousMap.toFun h)) }

end ProfiniteGrp

/-!
# Limits in the category of profinite groups

In this section, we construct limits in the category of profinite groups.

* `ProfiniteGrp.limitCone` : The explicit limit cone in `ProfiniteGrp`.

* `ProfiniteGrp.limitConeIsLimit`: `ProfiniteGrp.limitCone` is a limit cone.

-/

section Limits

namespace ProfiniteGrp

section

variable {J : Type v} [SmallCategory J] (F : J â¥¤ ProfiniteGrp.{max v u})

/-- Auxiliary construction to obtain the group structure on the limit of profinite groups. -/
def limitConePtAux : Subgroup (Î  j : J, F.obj j) where
  carrier := {x | âˆ€ â¦ƒi j : Jâ¦„ (Ï€ : i âŸ¶ j), F.map Ï€ (x i) = x j}
  mul_mem' hx hy _ _ Ï€ := by simp only [Pi.mul_apply, map_mul, hx Ï€, hy Ï€]
  one_mem' := by simp only [Set.mem_setOf_eq, Pi.one_apply, map_one, implies_true]
  inv_mem' h _ _ Ï€ := by simp only [Pi.inv_apply, map_inv, h Ï€]

instance : Group (Profinite.limitCone (F â‹™ profiniteGrpToProfinite.{max v u})).pt :=
  inferInstanceAs (Group (limitConePtAux F))

instance : TopologicalGroup (Profinite.limitCone (F â‹™ profiniteGrpToProfinite.{max v u})).pt :=
  inferInstanceAs (TopologicalGroup (limitConePtAux F))

/-- The explicit limit cone in `ProfiniteGrp`. -/
abbrev limitCone : Limits.Cone F where
  pt := ofProfinite (Profinite.limitCone (F â‹™ profiniteGrpToProfinite.{max v u})).pt
  Ï€ :=
  { app := fun j => {
      toFun := fun x => x.1 j
      map_one' := rfl
      map_mul' := fun x y => rfl
      continuous_toFun := by
        exact (continuous_apply j).comp (continuous_iff_le_induced.mpr fun U a => a) }
    naturality := fun i j f => by
      simp only [Functor.const_obj_obj, Functor.comp_obj,
        Functor.const_obj_map, Category.id_comp, Functor.comp_map]
      congr
      exact funext fun x => (x.2 f).symm }

/-- `ProfiniteGrp.limitCone` is a limit cone. -/
def limitConeIsLimit : Limits.IsLimit (limitCone F) where
  lift cone := {
    ((Profinite.limitConeIsLimit (F â‹™ profiniteGrpToProfinite)).lift
      (profiniteGrpToProfinite.mapCone cone)) with
    map_one' := Subtype.ext (funext fun j â†¦ map_one (cone.Ï€.app j))
    -- TODO: investigate whether it's possible to set up `ext` lemmas for the `TopCat`-related
    -- categories so that `by ext j; exact map_one (cone.Ï€.app j)` works here, similarly below.
    map_mul' := fun _ _ â†¦ Subtype.ext (funext fun j â†¦ map_mul (cone.Ï€.app j) _ _) }
  uniq cone m h := by
    apply profiniteGrpToProfinite.map_injective
    simpa using (Profinite.limitConeIsLimit (F â‹™ profiniteGrpToProfinite)).uniq
      (profiniteGrpToProfinite.mapCone cone) (profiniteGrpToProfinite.map m)
      (fun j â†¦ congrArg profiniteGrpToProfinite.map (h j))

instance : Limits.HasLimit F where
  exists_limit := Nonempty.intro
    { cone := limitCone F
      isLimit := limitConeIsLimit F }

/-- The abbreviation for the limit of `ProfiniteGrp`s. -/
abbrev limit : ProfiniteGrp := (ProfiniteGrp.limitCone F).pt

end

instance : Limits.PreservesLimits profiniteGrpToProfinite.{u} where
  preservesLimitsOfShape := {
    preservesLimit := fun {F} â†¦ CategoryTheory.Limits.preservesLimit_of_preserves_limit_cone
      (limitConeIsLimit F) (Profinite.limitConeIsLimit (F â‹™ profiniteGrpToProfinite)) }

end ProfiniteGrp

end Limits
