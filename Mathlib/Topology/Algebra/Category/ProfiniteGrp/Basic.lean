/-
Copyright (c) 2024 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Nailin Guan, Yuyang Zhao
-/
import Mathlib.Algebra.Category.Grp.FiniteGrp
import Mathlib.Topology.Algebra.Group.ClosedSubgroup
import Mathlib.Topology.Algebra.ContinuousMonoidHom
import Mathlib.Topology.Category.Profinite.Basic
import Mathlib.Topology.Separation.Connected
/-!

# Category of Profinite Groups

We say `G` is a profinite group if it is a topological group which is compact and totally
disconnected.

## Main definitions and results

* `ProfiniteGrp` is the category of profinite groups.

* `ProfiniteGrp.pi` : The pi-type of profinite groups is also a profinite group.

* `ofFiniteGrp` : A `FiniteGrp` when given the discrete topology can be considered as a
  profinite group.

* `ofClosedSubgroup` : A closed subgroup of a profinite group is profinite.

-/

universe u v

open CategoryTheory Topology

/--
The category of profinite groups. A term of this type consists of a profinite
set with a topological group structure.
-/
@[pp_with_univ]
structure ProfiniteGrp where
  /-- The underlying profinite topological space. -/
  toProfinite : Profinite
  /-- The group structure. -/
  [group : Group toProfinite]
  /-- The above data together form a topological group. -/
  [topologicalGroup : IsTopologicalGroup toProfinite]

/--
The category of profinite additive groups. A term of this type consists of a profinite
set with a topological additive group structure.
-/
@[pp_with_univ]
structure ProfiniteAddGrp where
  /-- The underlying profinite topological space. -/
  toProfinite : Profinite
  /-- The additive group structure. -/
  [addGroup : AddGroup toProfinite]
  /-- The above data together form a topological additive group. -/
  [topologicalAddGroup : IsTopologicalAddGroup toProfinite]

attribute [to_additive] ProfiniteGrp

@[to_additive]
instance : CoeSort ProfiniteGrp (Type u) where
  coe G := G.toProfinite

attribute [instance] ProfiniteGrp.group ProfiniteGrp.topologicalGroup
    ProfiniteAddGrp.addGroup ProfiniteAddGrp.topologicalAddGroup

/-- Construct a term of `ProfiniteGrp` from a type endowed with the structure of a
compact and totally disconnected topological group.
(The condition of being Hausdorff can be omitted here because totally disconnected implies that {1}
is a closed set, thus implying Hausdorff in a topological group.) -/
@[to_additive /-- Construct a term of `ProfiniteAddGrp` from a type endowed with the structure of a
compact and totally disconnected topological additive group.
(The condition of being Hausdorff can be omitted here because totally disconnected implies that {0}
is a closed set, thus implying Hausdorff in a topological additive group.) -/]
abbrev ProfiniteGrp.of (G : Type u) [Group G] [TopologicalSpace G] [IsTopologicalGroup G]
    [CompactSpace G] [TotallyDisconnectedSpace G] : ProfiniteGrp.{u} where
  toProfinite := .of G
  group := â€¹_â€º
  topologicalGroup := â€¹_â€º

@[to_additive]
lemma ProfiniteGrp.coe_of (G : Type u) [Group G] [TopologicalSpace G] [IsTopologicalGroup G]
    [CompactSpace G] [TotallyDisconnectedSpace G] : (ProfiniteGrp.of G : Type u) = G :=
  rfl

/-- The type of morphisms in `ProfiniteAddGrp`. -/
@[ext]
structure ProfiniteAddGrp.Hom (A B : ProfiniteAddGrp.{u}) where
  private mk ::
  /-- The underlying `ContinuousAddMonoidHom`. -/
  hom' : A â†’â‚œ+ B

/-- The type of morphisms in `ProfiniteGrp`. -/
@[to_additive existing (attr := ext)]
structure ProfiniteGrp.Hom (A B : ProfiniteGrp.{u}) where
  private mk ::
  /-- The underlying `ContinuousMonoidHom`. -/
  hom' : A â†’â‚œ* B

@[to_additive]
instance : Category ProfiniteGrp where
  Hom A B := ProfiniteGrp.Hom A B
  id A := âŸ¨ContinuousMonoidHom.id AâŸ©
  comp f g := âŸ¨g.hom'.comp f.hom'âŸ©

@[to_additive]
instance : ConcreteCategory ProfiniteGrp (fun X Y => X â†’â‚œ* Y) where
  hom f := f.hom'
  ofHom f := âŸ¨fâŸ©

/-- The underlying `ContinuousMonoidHom`. -/
@[to_additive /-- The underlying `ContinuousAddMonoidHom`. -/]
abbrev ProfiniteGrp.Hom.hom {M N : ProfiniteGrp.{u}} (f : ProfiniteGrp.Hom M N) :
    M â†’â‚œ* N :=
  ConcreteCategory.hom (C := ProfiniteGrp) f

/-- Typecheck a `ContinuousMonoidHom` as a morphism in `ProfiniteGrp`. -/
@[to_additive /-- Typecheck a `ContinuousAddMonoidHom` as a morphism in `ProfiniteAddGrp`. -/]
abbrev ProfiniteGrp.ofHom {X Y : Type u} [Group X] [TopologicalSpace X] [IsTopologicalGroup X]
    [CompactSpace X] [TotallyDisconnectedSpace X] [Group Y] [TopologicalSpace Y]
    [IsTopologicalGroup Y] [CompactSpace Y] [TotallyDisconnectedSpace Y]
    (f : X â†’â‚œ* Y) : ProfiniteGrp.of X âŸ¶ ProfiniteGrp.of Y :=
  ConcreteCategory.ofHom f

namespace ProfiniteGrp

@[to_additive]
instance {M N : ProfiniteGrp.{u}} : CoeFun (M âŸ¶ N) (fun _ â†¦ M â†’ N) where
  coe f := f.hom

@[to_additive (attr := simp)]
lemma hom_id {A : ProfiniteGrp.{u}} : (ðŸ™ A : A âŸ¶ A).hom = ContinuousMonoidHom.id A := rfl

/- Provided for rewriting. -/
@[to_additive]
lemma id_apply (A : ProfiniteGrp.{u}) (a : A) :
    (ðŸ™ A : A âŸ¶ A) a = a := by simp

@[to_additive (attr := simp)]
lemma hom_comp {A B C : ProfiniteGrp.{u}} (f : A âŸ¶ B) (g : B âŸ¶ C) :
    (f â‰« g).hom = g.hom.comp f.hom := rfl

/- Provided for rewriting. -/
@[to_additive]
lemma comp_apply {A B C : ProfiniteGrp.{u}} (f : A âŸ¶ B) (g : B âŸ¶ C) (a : A) :
    (f â‰« g) a = g (f a) := by
  simp only [hom_comp, ContinuousMonoidHom.comp_toFun]

@[to_additive (attr := ext)]
lemma hom_ext {A B : ProfiniteGrp.{u}} {f g : A âŸ¶ B} (hf : f.hom = g.hom) : f = g :=
  Hom.ext hf

variable {X Y Z : Type u} [Group X] [TopologicalSpace X] [IsTopologicalGroup X]
    [CompactSpace X] [TotallyDisconnectedSpace X] [Group Y] [TopologicalSpace Y]
    [IsTopologicalGroup Y] [CompactSpace Y] [TotallyDisconnectedSpace Y] [Group Z]
    [TopologicalSpace Z] [IsTopologicalGroup Z] [CompactSpace Z] [TotallyDisconnectedSpace Z]

@[to_additive (attr := simp)]
lemma hom_ofHom (f : X â†’â‚œ* Y) : (ofHom f).hom = f := rfl

@[to_additive (attr := simp)]
lemma ofHom_hom {A B : ProfiniteGrp.{u}} (f : A âŸ¶ B) :
    ofHom (Hom.hom f) = f := rfl

@[to_additive (attr := simp)]
lemma ofHom_id : ofHom (ContinuousMonoidHom.id X) = ðŸ™ (of X) := rfl

@[to_additive (attr := simp)]
lemma ofHom_comp (f : X â†’â‚œ* Y) (g : Y â†’â‚œ* Z) :
    ofHom (g.comp f) = ofHom f â‰« ofHom g :=
  rfl

@[to_additive]
lemma ofHom_apply (f : X â†’â‚œ* Y) (x : X) : ofHom f x = f x := rfl

@[to_additive]
lemma inv_hom_apply {A B : ProfiniteGrp.{u}} (e : A â‰… B) (x : A) : e.inv (e.hom x) = x := by
  simp

@[to_additive]
lemma hom_inv_apply {A B : ProfiniteGrp.{u}} (e : A â‰… B) (x : B) : e.hom (e.inv x) = x := by
  simp

@[to_additive (attr := simp)]
theorem coe_id (X : ProfiniteGrp) : (ðŸ™ X : X â†’ X) = id :=
  rfl

@[to_additive (attr := simp)]
theorem coe_comp {X Y Z : ProfiniteGrp} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    (f â‰« g : X â†’ Z) = g âˆ˜ f :=
  rfl

/-- Construct a term of `ProfiniteGrp` from a type endowed with the structure of a
profinite topological group. -/
@[to_additive /-- Construct a term of `ProfiniteAddGrp` from a type endowed with the structure of a
profinite topological additive group. -/]
abbrev ofProfinite (G : Profinite) [Group G] [IsTopologicalGroup G] :
    ProfiniteGrp := of G

/-- The pi-type of profinite groups is a profinite group. -/
@[to_additive /-- The pi-type of profinite additive groups is a
profinite additive group. -/]
def pi {Î± : Type u} (Î² : Î± â†’ ProfiniteGrp) : ProfiniteGrp :=
  let pitype := Profinite.pi fun (a : Î±) => (Î² a).toProfinite
  letI (a : Î±): Group (Î² a).toProfinite := (Î² a).group
  letI : Group pitype := Pi.group
  letI : IsTopologicalGroup pitype := Pi.topologicalGroup
  ofProfinite pitype

/-- A `FiniteGrp` when given the discrete topology can be considered as a profinite group. -/
@[to_additive /-- A `FiniteAddGrp` when given the discrete topology can be considered as a
profinite additive group. -/]
def ofFiniteGrp (G : FiniteGrp) : ProfiniteGrp :=
  letI : TopologicalSpace G := âŠ¥
  letI : DiscreteTopology G := âŸ¨rflâŸ©
  letI : IsTopologicalGroup G := {}
  of G

@[to_additive]
instance : HasForgetâ‚‚ FiniteGrp ProfiniteGrp where
  forgetâ‚‚ :=
  { obj := ofFiniteGrp
    map := fun f => âŸ¨f.hom, by continuityâŸ© }

@[to_additive]
instance : HasForgetâ‚‚ ProfiniteGrp GrpCat where
  forgetâ‚‚.obj P := GrpCat.of P
  forgetâ‚‚.map f := GrpCat.ofHom f.hom.toMonoidHom

/-- A closed subgroup of a profinite group is profinite. -/
@[to_additive /-- A closed additive subgroup of a profinite additive group is profinite. -/]
def ofClosedSubgroup {G : ProfiniteGrp} (H : ClosedSubgroup G) : ProfiniteGrp :=
  letI : CompactSpace H := inferInstance
  of H.1

/-- A topological group that has a `ContinuousMulEquiv` to a profinite group is profinite. -/
@[to_additive /-- A topological additive group that has a `ContinuousAddEquiv` to a
profinite additive group is profinite. -/]
def ofContinuousMulEquiv {G : ProfiniteGrp.{u}} {H : Type v} [TopologicalSpace H]
    [Group H] [IsTopologicalGroup H] (e : G â‰ƒâ‚œ* H) : ProfiniteGrp.{v} :=
  let _ : CompactSpace H := Homeomorph.compactSpace e.toHomeomorph
  let _ : TotallyDisconnectedSpace H := Homeomorph.totallyDisconnectedSpace e.toHomeomorph
  .of H

/-- Build an isomorphism in the category `ProfiniteGrp` from
a `ContinuousMulEquiv` between `ProfiniteGrp`s. -/
def ContinuousMulEquiv.toProfiniteGrpIso {X Y : ProfiniteGrp} (e : X â‰ƒâ‚œ* Y) : X â‰… Y where
  hom := ofHom e
  inv := ofHom e.symm

/-- The functor mapping a profinite group to its underlying profinite space. -/
@[to_additive]
instance : HasForgetâ‚‚ ProfiniteGrp Profinite where
  forgetâ‚‚ := {
    obj G := G.toProfinite
    map f := CompHausLike.ofHom _ âŸ¨f, by continuityâŸ©}

@[to_additive]
instance : (forgetâ‚‚ ProfiniteGrp Profinite).Faithful := {
  map_injective := fun {_ _} _ _ h =>
    ConcreteCategory.hom_ext _ _ (CategoryTheory.congr_fun h) }

instance : (forgetâ‚‚ ProfiniteGrp Profinite).ReflectsIsomorphisms where
  reflects {X Y} f _ := by
    let i := asIso ((forgetâ‚‚ ProfiniteGrp Profinite).map f)
    let e : X â‰ƒâ‚œ* Y :=
      { CompHausLike.homeoOfIso i with
          map_mul' := map_mul f.hom }
    exact (ContinuousMulEquiv.toProfiniteGrpIso e).isIso_hom

instance : (forget ProfiniteGrp.{u}).ReflectsIsomorphisms :=
  CategoryTheory.reflectsIsomorphisms_comp (forgetâ‚‚ ProfiniteGrp Profinite) (forget Profinite)

end ProfiniteGrp

/-!
# Limits in the category of profinite groups

In this section, we construct limits in the category of profinite groups.

* `ProfiniteGrp.limitCone` : The explicit limit cone in `ProfiniteGrp`.

* `ProfiniteGrp.limitConeIsLimit`: `ProfiniteGrp.limitCone` is a limit cone.

-/

section Limits

namespace ProfiniteGrp

variable {J : Type v} [SmallCategory J] (F : J â¥¤ ProfiniteGrp.{max v u})

/-- Auxiliary construction to obtain the group structure on the limit of profinite groups. -/
@[to_additive /-- Auxiliary construction to obtain the additive group structure on the limit of
profinite additive groups. -/]
def limitConePtAux : Subgroup (Î  j : J, F.obj j) where
  carrier := {x | âˆ€ â¦ƒi j : Jâ¦„ (Ï€ : i âŸ¶ j), F.map Ï€ (x i) = x j}
  mul_mem' hx hy _ _ Ï€ := by simp only [Pi.mul_apply, map_mul, hx Ï€, hy Ï€]
  one_mem' := by simp only [Set.mem_setOf_eq, Pi.one_apply, map_one, implies_true]
  inv_mem' h _ _ Ï€ := by simp only [Pi.inv_apply, map_inv, h Ï€]

@[to_additive]
instance : Group (Profinite.limitCone (F â‹™ (forgetâ‚‚ ProfiniteGrp Profinite))).pt :=
  inferInstanceAs (Group (limitConePtAux F))

@[to_additive]
instance : IsTopologicalGroup (Profinite.limitCone (F â‹™ (forgetâ‚‚ ProfiniteGrp Profinite))).pt :=
  inferInstanceAs (IsTopologicalGroup (limitConePtAux F))

/-- The explicit limit cone in `ProfiniteGrp`. -/
@[to_additive /-- The explicit limit cone in `ProfiniteAddGrp`. -/]
abbrev limitCone : Limits.Cone F where
  pt := ofProfinite (Profinite.limitCone (F â‹™ (forgetâ‚‚ ProfiniteGrp Profinite))).pt
  Ï€ :=
  { app := fun j => âŸ¨{
      toFun := fun x => x.1 j
      map_one' := rfl
      map_mul' := fun x y => rfl
      continuous_toFun := by
        exact (continuous_apply j).comp (continuous_iff_le_induced.mpr fun U a => a) }âŸ©
    naturality := fun i j f => by
      simp only [Functor.const_obj_obj, Functor.comp_obj,
        Functor.const_obj_map, Category.id_comp, Functor.comp_map]
      congr
      exact funext fun x => (x.2 f).symm }

/-- `ProfiniteGrp.limitCone` is a limit cone. -/
@[to_additive /-- `ProfiniteAddGrp.limitCone` is a limit cone. -/]
def limitConeIsLimit : Limits.IsLimit (limitCone F) where
  lift cone := ofHom
    { ((Profinite.limitConeIsLimit (F â‹™ (forgetâ‚‚ ProfiniteGrp Profinite))).lift
        ((forgetâ‚‚ ProfiniteGrp Profinite).mapCone cone)).hom with
      map_one' := Subtype.ext (funext fun j â†¦ map_one (cone.Ï€.app j).hom)
      -- TODO: investigate whether it's possible to set up `ext` lemmas for the `TopCat`-related
      -- categories so that `by ext j; exact map_one (cone.Ï€.app j)` works here, similarly below.
      map_mul' := fun _ _ â†¦ Subtype.ext (funext fun j â†¦ map_mul (cone.Ï€.app j).hom _ _) }
  uniq cone m h := by
    apply (forgetâ‚‚ ProfiniteGrp Profinite).map_injective
    simpa using (Profinite.limitConeIsLimit (F â‹™ (forgetâ‚‚ ProfiniteGrp Profinite))).uniq
      ((forgetâ‚‚ ProfiniteGrp Profinite).mapCone cone) ((forgetâ‚‚ ProfiniteGrp Profinite).map m)
      (fun j â†¦ congrArg (forgetâ‚‚ ProfiniteGrp Profinite).map (h j))

@[to_additive]
instance : Limits.HasLimit F where
  exists_limit := Nonempty.intro
    { cone := limitCone F
      isLimit := limitConeIsLimit F }

@[to_additive]
instance : Limits.PreservesLimits (forgetâ‚‚ ProfiniteGrp Profinite) where
  preservesLimitsOfShape := {
    preservesLimit := fun {F} â†¦ CategoryTheory.Limits.preservesLimit_of_preserves_limit_cone
      (limitConeIsLimit F) (Profinite.limitConeIsLimit (F â‹™ (forgetâ‚‚ ProfiniteGrp Profinite))) }

@[to_additive]
instance : CompactSpace (limitConePtAux F) :=
  inferInstanceAs (CompactSpace (Profinite.limitCone (F â‹™ (forgetâ‚‚ ProfiniteGrp Profinite))).pt)

/-- The abbreviation for the limit of `ProfiniteGrp`s. -/
@[to_additive /-- The abbreviation for the limit of `ProfiniteAddGrp`s. -/]
abbrev limit : ProfiniteGrp := ProfiniteGrp.of (ProfiniteGrp.limitConePtAux F)

@[to_additive (attr := ext)]
lemma limit_ext (x y : limit F) (hxy : âˆ€ j, x.val j = y.val j) : x = y :=
  Subtype.ext (funext hxy)

@[to_additive (attr := simp)]
lemma limit_one_val (j : J) : (1 : limit F).val j = 1 :=
  rfl

@[to_additive (attr := simp)]
lemma limit_mul_val (x y : limit F) (j : J) : (x * y).val j = x.val j * y.val j :=
  rfl

end ProfiniteGrp

end Limits
