/-
Copyright (c) 2022 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn, Patrick Massot
-/
import Mathlib.Algebra.GroupWithZero.Indicator
import Mathlib.Algebra.Module.Basic
import Mathlib.Algebra.Order.Group.Unbundled.Abs
import Mathlib.Topology.Homeomorph.Defs
import Mathlib.Topology.Separation.Hausdorff

/-!
# The topological support of a function

In this file we define the topological support of a function `f`, `tsupport f`, as the closure of
the support of `f`.

Furthermore, we say that `f` has compact support if the topological support of `f` is compact.

## Main definitions

* `mulTSupport` & `tsupport`
* `HasCompactMulSupport` & `HasCompactSupport`

## TODO

The definitions have been put in the root namespace following many other topological definitions,
like `Embedding`. Since then, `Embedding` was renamed to `Topology.IsEmbedding`, so it might be
worth reconsidering namespacing the definitions here.
-/


open Function Set Filter Topology

variable {X Œ± Œ±' Œ≤ Œ≥ Œ¥ M R : Type*}

section One

variable [One Œ±] [TopologicalSpace X]

/-- The topological support of a function is the closure of its support, i.e. the closure of the
set of all elements where the function is not equal to 1. -/
@[to_additive /-- The topological support of a function is the closure of its support. i.e. the
closure of the set of all elements where the function is nonzero. -/]
def mulTSupport (f : X ‚Üí Œ±) : Set X := closure (mulSupport f)

@[to_additive]
theorem subset_mulTSupport (f : X ‚Üí Œ±) : mulSupport f ‚äÜ mulTSupport f :=
  subset_closure

@[to_additive]
theorem isClosed_mulTSupport (f : X ‚Üí Œ±) : IsClosed (mulTSupport f) :=
  isClosed_closure

@[to_additive]
theorem mulTSupport_eq_empty_iff {f : X ‚Üí Œ±} : mulTSupport f = ‚àÖ ‚Üî f = 1 := by
  rw [mulTSupport, closure_empty_iff, mulSupport_eq_empty_iff]

@[to_additive]
theorem image_eq_one_of_notMem_mulTSupport {f : X ‚Üí Œ±} {x : X} (hx : x ‚àâ mulTSupport f) : f x = 1 :=
  mulSupport_subset_iff'.mp (subset_mulTSupport f) x hx

@[deprecated (since := "2025-05-24")]
alias image_eq_zero_of_nmem_tsupport := image_eq_zero_of_notMem_tsupport

@[to_additive existing, deprecated (since := "2025-05-24")]
alias image_eq_one_of_nmem_mulTSupport := image_eq_one_of_notMem_mulTSupport

@[to_additive]
theorem range_subset_insert_image_mulTSupport (f : X ‚Üí Œ±) :
    range f ‚äÜ insert 1 (f '' mulTSupport f) := by
  grw [‚Üê subset_mulTSupport f]; exact range_subset_insert_image_mulSupport f

@[to_additive]
theorem range_eq_image_mulTSupport_or (f : X ‚Üí Œ±) :
    range f = f '' mulTSupport f ‚à® range f = insert 1 (f '' mulTSupport f) :=
  (wcovBy_insert _ _).eq_or_eq (image_subset_range _ _) (range_subset_insert_image_mulTSupport f)

theorem tsupport_mul_subset_left {Œ± : Type*} [MulZeroClass Œ±] {f g : X ‚Üí Œ±} :
    (tsupport fun x => f x * g x) ‚äÜ tsupport f :=
  closure_mono (support_mul_subset_left _ _)

theorem tsupport_mul_subset_right {Œ± : Type*} [MulZeroClass Œ±] {f g : X ‚Üí Œ±} :
    (tsupport fun x => f x * g x) ‚äÜ tsupport g :=
  closure_mono (support_mul_subset_right _ _)

end One

theorem tsupport_smul_subset_left {M Œ±} [TopologicalSpace X] [Zero M] [Zero Œ±] [SMulWithZero M Œ±]
    (f : X ‚Üí M) (g : X ‚Üí Œ±) : (tsupport fun x => f x ‚Ä¢ g x) ‚äÜ tsupport f :=
  closure_mono <| support_smul_subset_left f g

theorem tsupport_smul_subset_right {M Œ±} [TopologicalSpace X] [Zero Œ±] [SMulZeroClass M Œ±]
    (f : X ‚Üí M) (g : X ‚Üí Œ±) : (tsupport fun x => f x ‚Ä¢ g x) ‚äÜ tsupport g :=
  closure_mono <| support_smul_subset_right f g

@[to_additive]
theorem mulTSupport_mul [TopologicalSpace X] [MulOneClass Œ±] {f g : X ‚Üí Œ±} :
    (mulTSupport fun x ‚Ü¶ f x * g x) ‚äÜ mulTSupport f ‚à™ mulTSupport g :=
  closure_minimal
    ((mulSupport_mul f g).trans (union_subset_union (subset_mulTSupport _) (subset_mulTSupport _)))
    (isClosed_closure.union isClosed_closure)

section

variable [TopologicalSpace Œ±]
variable [One Œ≤]
variable {f : Œ± ‚Üí Œ≤} {x : Œ±}

@[to_additive]
theorem notMem_mulTSupport_iff_eventuallyEq : x ‚àâ mulTSupport f ‚Üî f =·∂†[ùìù x] 1 := by
  simp_rw [mulTSupport, mem_closure_iff_nhds, not_forall, not_nonempty_iff_eq_empty, exists_prop,
    ‚Üê disjoint_iff_inter_eq_empty, disjoint_mulSupport_iff, eventuallyEq_iff_exists_mem]

@[deprecated (since := "2025-05-23")]
alias not_mem_tsupport_iff_eventuallyEq := notMem_tsupport_iff_eventuallyEq

@[to_additive existing, deprecated (since := "2025-05-23")]
alias not_mem_mulTSupport_iff_eventuallyEq := notMem_mulTSupport_iff_eventuallyEq

@[to_additive]
theorem continuous_of_mulTSupport [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : ‚àÄ x ‚àà mulTSupport f, ContinuousAt f x) : Continuous f :=
  continuous_iff_continuousAt.2 fun x => (em _).elim (hf x) fun hx =>
    (@continuousAt_const _ _ _ _ _ 1).congr (notMem_mulTSupport_iff_eventuallyEq.mp hx).symm

@[to_additive]
lemma ContinuousOn.continuous_of_mulTSupport_subset [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤}
    {s : Set Œ±} (hs : ContinuousOn f s) (h's : IsOpen s) (h''s : mulTSupport f ‚äÜ s) :
    Continuous f :=
  continuous_of_mulTSupport fun _ hx ‚Ü¶ h's.continuousOn_iff.mp hs <| h''s hx

end

/-! ## Functions with compact support -/
section CompactSupport

variable [TopologicalSpace Œ±] [TopologicalSpace Œ±'] [One Œ≤] [One Œ≥] [One Œ¥]
  {g : Œ≤ ‚Üí Œ≥} {f : Œ± ‚Üí Œ≤} {f‚ÇÇ : Œ± ‚Üí Œ≥} {m : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥}

/-- A function `f` *has compact multiplicative support* or is *compactly supported* if the closure
of the multiplicative support of `f` is compact. In a T‚ÇÇ space this is equivalent to `f` being equal
to `1` outside a compact set. -/
@[to_additive /-- A function `f` *has compact support* or is *compactly supported* if the closure of
the support of `f` is compact. In a T‚ÇÇ space this is equivalent to `f` being equal to `0` outside a
compact set. -/]
def HasCompactMulSupport (f : Œ± ‚Üí Œ≤) : Prop :=
  IsCompact (mulTSupport f)

@[to_additive]
theorem hasCompactMulSupport_def : HasCompactMulSupport f ‚Üî IsCompact (closure (mulSupport f)) := by
  rfl

@[to_additive]
theorem exists_compact_iff_hasCompactMulSupport [R1Space Œ±] :
    (‚àÉ K : Set Œ±, IsCompact K ‚àß ‚àÄ x, x ‚àâ K ‚Üí f x = 1) ‚Üî HasCompactMulSupport f := by
  simp_rw [‚Üê notMem_mulSupport, ‚Üê mem_compl_iff, ‚Üê subset_def, compl_subset_compl,
    hasCompactMulSupport_def, exists_isCompact_superset_iff]

namespace HasCompactMulSupport

variable {K : Set Œ±}

@[to_additive]
theorem intro [R1Space Œ±] (hK : IsCompact K) (hfK : ‚àÄ x, x ‚àâ K ‚Üí f x = 1) :
    HasCompactMulSupport f :=
  exists_compact_iff_hasCompactMulSupport.mp ‚ü®K, hK, hfK‚ü©

@[to_additive]
theorem intro' (hK : IsCompact K) (h'K : IsClosed K) (hfK : ‚àÄ x, x ‚àâ K ‚Üí f x = 1) :
    HasCompactMulSupport f := by
  have : mulTSupport f ‚äÜ K := by
    rw [‚Üê h'K.closure_eq]
    apply closure_mono (mulSupport_subset_iff'.2 hfK)
  exact IsCompact.of_isClosed_subset hK ( isClosed_mulTSupport f) this

@[to_additive]
theorem of_mulSupport_subset_isCompact [R1Space Œ±] (hK : IsCompact K) (h : mulSupport f ‚äÜ K) :
    HasCompactMulSupport f :=
  hK.closure_of_subset h

@[to_additive]
theorem isCompact (hf : HasCompactMulSupport f) : IsCompact (mulTSupport f) := hf

@[to_additive]
theorem _root_.hasCompactMulSupport_iff_eventuallyEq :
    HasCompactMulSupport f ‚Üî f =·∂†[coclosedCompact Œ±] 1 :=
  mem_coclosedCompact_iff.symm

@[to_additive]
theorem _root_.isCompact_range_of_mulSupport_subset_isCompact [TopologicalSpace Œ≤]
    (hf : Continuous f) (hk : IsCompact K) (h'f : mulSupport f ‚äÜ K) :
    IsCompact (range f) := by
  rcases range_eq_image_or_of_mulSupport_subset h'f with h2 | h2 <;> rw [h2]
  exacts [hk.image hf, (hk.image hf).insert 1]

@[to_additive]
theorem isCompact_range [TopologicalSpace Œ≤] (h : HasCompactMulSupport f)
    (hf : Continuous f) : IsCompact (range f) :=
  isCompact_range_of_mulSupport_subset_isCompact hf h (subset_mulTSupport f)

@[to_additive]
theorem mono' {f' : Œ± ‚Üí Œ≥} (hf : HasCompactMulSupport f) (hff' : mulSupport f' ‚äÜ mulTSupport f) :
    HasCompactMulSupport f' :=
  IsCompact.of_isClosed_subset hf isClosed_closure <| closure_minimal hff' isClosed_closure

@[to_additive]
theorem mono {f' : Œ± ‚Üí Œ≥} (hf : HasCompactMulSupport f) (hff' : mulSupport f' ‚äÜ mulSupport f) :
    HasCompactMulSupport f' :=
  hf.mono' <| hff'.trans subset_closure

@[to_additive]
theorem comp_left (hf : HasCompactMulSupport f) (hg : g 1 = 1) :
    HasCompactMulSupport (g ‚àò f) :=
  hf.mono <| mulSupport_comp_subset hg f

@[to_additive]
theorem _root_.hasCompactMulSupport_comp_left (hg : ‚àÄ {x}, g x = 1 ‚Üî x = 1) :
    HasCompactMulSupport (g ‚àò f) ‚Üî HasCompactMulSupport f := by
  simp_rw [hasCompactMulSupport_def, mulSupport_comp_eq g (@hg) f]

@[to_additive]
theorem comp_isClosedEmbedding (hf : HasCompactMulSupport f) {g : Œ±' ‚Üí Œ±}
    (hg : IsClosedEmbedding g) : HasCompactMulSupport (f ‚àò g) := by
  rw [hasCompactMulSupport_def, Function.mulSupport_comp_eq_preimage]
  refine IsCompact.of_isClosed_subset (hg.isCompact_preimage hf) isClosed_closure ?_
  rw [hg.isEmbedding.closure_eq_preimage_closure_image]
  exact preimage_mono (closure_mono <| image_preimage_subset _ _)

@[to_additive]
theorem comp‚ÇÇ_left (hf : HasCompactMulSupport f)
    (hf‚ÇÇ : HasCompactMulSupport f‚ÇÇ) (hm : m 1 1 = 1) :
    HasCompactMulSupport fun x => m (f x) (f‚ÇÇ x) := by
  rw [hasCompactMulSupport_iff_eventuallyEq] at hf hf‚ÇÇ ‚ä¢
  filter_upwards [hf, hf‚ÇÇ] with x hx hx‚ÇÇ
  simp_rw [hx, hx‚ÇÇ, Pi.one_apply, hm]

@[to_additive]
lemma isCompact_preimage [TopologicalSpace Œ≤] {K : Set Œ≤}
    (h'f : HasCompactMulSupport f) (hf : Continuous f) (hk : IsClosed K) (h'k : 1 ‚àâ K) :
    IsCompact (f ‚Åª¬π' K) := by
  apply IsCompact.of_isClosed_subset h'f (hk.preimage hf) (fun x hx ‚Ü¶ ?_)
  apply subset_mulTSupport
  aesop

variable [T2Space Œ±']

section

variable (hf : HasCompactMulSupport f) {g : Œ± ‚Üí Œ±'} (cont : Continuous g)
include hf cont

@[to_additive]
theorem mulTSupport_extend_one_subset :
    mulTSupport (g.extend f 1) ‚äÜ g '' mulTSupport f :=
  (hf.image cont).isClosed.closure_subset_iff.mpr <|
    mulSupport_extend_one_subset.trans (image_mono subset_closure)

@[to_additive]
theorem extend_one : HasCompactMulSupport (g.extend f 1) :=
  HasCompactMulSupport.of_mulSupport_subset_isCompact (hf.image cont)
    (subset_closure.trans <| hf.mulTSupport_extend_one_subset cont)

@[to_additive]
theorem mulTSupport_extend_one (inj : g.Injective) :
    mulTSupport (g.extend f 1) = g '' mulTSupport f :=
  (hf.mulTSupport_extend_one_subset cont).antisymm <|
    (image_closure_subset_closure_image cont).trans
      (closure_mono (mulSupport_extend_one inj).superset)

end

@[to_additive]
theorem continuous_extend_one [TopologicalSpace Œ≤] {U : Set Œ±'} (hU : IsOpen U) {f : U ‚Üí Œ≤}
    (cont : Continuous f) (supp : HasCompactMulSupport f) :
    Continuous (Subtype.val.extend f 1) :=
  continuous_of_mulTSupport fun x h ‚Ü¶ by
    rw [show x = ‚Üë(‚ü®x, Subtype.coe_image_subset _ _
      (supp.mulTSupport_extend_one_subset continuous_subtype_val h)‚ü© : U) by rfl,
      ‚Üê (hU.isOpenEmbedding_subtypeVal).continuousAt_iff, extend_comp Subtype.val_injective]
    exact cont.continuousAt

/-- If `f` has compact multiplicative support, then `f` tends to 1 at infinity. -/
@[to_additive /-- If `f` has compact support, then `f` tends to zero at infinity. -/]
theorem is_one_at_infty {f : Œ± ‚Üí Œ≥} [TopologicalSpace Œ≥]
    (h : HasCompactMulSupport f) : Tendsto f (cocompact Œ±) (ùìù 1) := by
  intro N hN
  rw [mem_map, mem_cocompact']
  refine ‚ü®mulTSupport f, h.isCompact, ?_‚ü©
  rw [compl_subset_comm]
  intro v hv
  rw [mem_preimage, image_eq_one_of_notMem_mulTSupport hv]
  exact mem_of_mem_nhds hN

end HasCompactMulSupport

section Compact

variable [CompactSpace Œ±]

/-- In a compact space `Œ±`, any function has compact support. -/
@[to_additive]
theorem HasCompactMulSupport.of_compactSpace (f : Œ± ‚Üí Œ≥) :
    HasCompactMulSupport f :=
  IsCompact.of_isClosed_subset isCompact_univ (isClosed_mulTSupport f)
    (Set.subset_univ (mulTSupport f))

end Compact

end CompactSupport

/-! ## Functions with compact support: algebraic operations -/
section CompactSupport2
section Monoid

variable [TopologicalSpace Œ±] [MulOneClass Œ≤]
variable {f f' : Œ± ‚Üí Œ≤}

@[to_additive]
theorem HasCompactMulSupport.mul (hf : HasCompactMulSupport f) (hf' : HasCompactMulSupport f') :
    HasCompactMulSupport (f * f') := hf.comp‚ÇÇ_left hf' (mul_one 1)

@[to_additive, simp]
protected lemma HasCompactMulSupport.one {Œ± Œ≤ : Type*} [TopologicalSpace Œ±] [One Œ≤] :
    HasCompactMulSupport (1 : Œ± ‚Üí Œ≤) := by
  simp [HasCompactMulSupport, mulTSupport]

end Monoid

section DivisionMonoid

@[to_additive]
protected lemma HasCompactMulSupport.inv {Œ± Œ≤ : Type*} [TopologicalSpace Œ±] [DivisionMonoid Œ≤]
    {f : Œ± ‚Üí Œ≤} (hf : HasCompactMulSupport f) :
    HasCompactMulSupport (f‚Åª¬π) := by
  simpa only [HasCompactMulSupport, mulTSupport, mulSupport_inv] using hf

@[deprecated (since := "2025-07-31")] alias HasCompactSupport.neg' := HasCompactSupport.neg
@[deprecated (since := "2025-07-31")] alias HasCompactMulSupport.inv' := HasCompactMulSupport.inv

@[to_additive]
theorem HasCompactSupport.div {Œ± Œ≤ : Type*} [TopologicalSpace Œ±] [DivisionMonoid Œ≤]
    {f f' : Œ± ‚Üí Œ≤} (hf : HasCompactMulSupport f) (hf' : HasCompactMulSupport f') :
    HasCompactMulSupport (f / f') :=
  div_eq_mul_inv f f' ‚ñ∏ hf.mul hf'.inv

end DivisionMonoid

section SMulZeroClass

variable [TopologicalSpace Œ±] [Zero M] [SMulZeroClass R M]
variable {f : Œ± ‚Üí R} {f' : Œ± ‚Üí M}

theorem HasCompactSupport.smul_left (hf : HasCompactSupport f') : HasCompactSupport (f ‚Ä¢ f') := by
  rw [hasCompactSupport_iff_eventuallyEq] at hf ‚ä¢
  exact hf.mono fun x hx => by simp_rw [Pi.smul_apply', hx, Pi.zero_apply, smul_zero]

end SMulZeroClass

section SMulWithZero

variable [TopologicalSpace Œ±] [Zero R] [Zero M] [SMulWithZero R M]
variable {f : Œ± ‚Üí R} {f' : Œ± ‚Üí M}

theorem HasCompactSupport.smul_right (hf : HasCompactSupport f) : HasCompactSupport (f ‚Ä¢ f') := by
  rw [hasCompactSupport_iff_eventuallyEq] at hf ‚ä¢
  exact hf.mono fun x hx => by simp_rw [Pi.smul_apply', hx, Pi.zero_apply, zero_smul]

end SMulWithZero

section MulZeroClass

variable [TopologicalSpace Œ±] [MulZeroClass Œ≤]
variable {f f' : Œ± ‚Üí Œ≤}

theorem HasCompactSupport.mul_right (hf : HasCompactSupport f) : HasCompactSupport (f * f') := by
  rw [hasCompactSupport_iff_eventuallyEq] at hf ‚ä¢
  exact hf.mono fun x hx => by simp_rw [Pi.mul_apply, hx, Pi.zero_apply, zero_mul]

theorem HasCompactSupport.mul_left (hf : HasCompactSupport f') : HasCompactSupport (f * f') := by
  rw [hasCompactSupport_iff_eventuallyEq] at hf ‚ä¢
  exact hf.mono fun x hx => by simp_rw [Pi.mul_apply, hx, Pi.zero_apply, mul_zero]

end MulZeroClass

section OrderedAddGroup

variable [TopologicalSpace Œ±] [AddGroup Œ≤] [Lattice Œ≤] [AddLeftMono Œ≤]

protected theorem HasCompactSupport.abs {f : Œ± ‚Üí Œ≤} (hf : HasCompactSupport f) :
    HasCompactSupport |f| :=
  hf.comp_left (g := abs) abs_zero

end OrderedAddGroup

end CompactSupport2

section LocallyFinite

variable {Œπ : Type*} [TopologicalSpace X]

-- TODO: reformulate for any locally finite family of sets
/-- If a family of functions `f` has locally-finite multiplicative support, subordinate to a family
of open sets, then for any point we can find a neighbourhood on which only finitely-many members of
`f` are not equal to 1. -/
@[to_additive /-- If a family of functions `f` has locally-finite support, subordinate to a family
of open sets, then for any point we can find a neighbourhood on which only finitely-many members of
`f` are non-zero. -/]
theorem LocallyFinite.exists_finset_nhds_mulSupport_subset {U : Œπ ‚Üí Set X} [One R] {f : Œπ ‚Üí X ‚Üí R}
    (hlf : LocallyFinite fun i => mulSupport (f i)) (hso : ‚àÄ i, mulTSupport (f i) ‚äÜ U i)
    (ho : ‚àÄ i, IsOpen (U i)) (x : X) :
    ‚àÉ (is : Finset Œπ), ‚àÉ n, n ‚àà ùìù x ‚àß (n ‚äÜ ‚ãÇ i ‚àà is, U i) ‚àß
      ‚àÄ z ‚àà n, (mulSupport fun i => f i z) ‚äÜ is := by
  obtain ‚ü®n, hn, hnf‚ü© := hlf x
  classical
    let is := {i ‚àà hnf.toFinset | x ‚àà U i}
    let js := {j ‚àà hnf.toFinset | x ‚àâ U j}
    refine
      ‚ü®is, (n ‚à© ‚ãÇ j ‚àà js, (mulTSupport (f j))·∂ú) ‚à© ‚ãÇ i ‚àà is, U i, inter_mem (inter_mem hn ?_) ?_,
        inter_subset_right, fun z hz => ?_‚ü©
    ¬∑ exact (biInter_finset_mem js).mpr fun j hj => IsClosed.compl_mem_nhds (isClosed_mulTSupport _)
        (Set.notMem_subset (hso j) (Finset.mem_filter.mp hj).2)
    ¬∑ exact (biInter_finset_mem is).mpr fun i hi => (ho i).mem_nhds (Finset.mem_filter.mp hi).2
    ¬∑ have hzn : z ‚àà n := by
        rw [inter_assoc] at hz
        exact mem_of_mem_inter_left hz
      replace hz := mem_of_mem_inter_right (mem_of_mem_inter_left hz)
      simp only [js, Finset.mem_filter, Finite.mem_toFinset, mem_setOf_eq, mem_iInter,
        and_imp] at hz
      suffices (mulSupport fun i => f i z) ‚äÜ hnf.toFinset by
        refine hnf.toFinset.subset_coe_filter_of_subset_forall _ this fun i hi => ?_
        specialize hz i ‚ü®z, ‚ü®hi, hzn‚ü©‚ü©
        contrapose hz
        simp [hz, subset_mulTSupport (f i) hi]
      intro i hi
      simp only [Finite.coe_toFinset, mem_setOf_eq]
      exact ‚ü®z, ‚ü®hi, hzn‚ü©‚ü©

@[deprecated (since := "2025-05-22")]
alias LocallyFinite.exists_finset_nhd_mulSupport_subset :=
  LocallyFinite.exists_finset_nhds_mulSupport_subset

@[deprecated (since := "2025-05-22")]
alias LocallyFinite.exists_finset_nhd_support_subset :=
  LocallyFinite.exists_finset_nhds_support_subset

@[to_additive]
theorem locallyFinite_mulSupport_iff [One M] {f : Œπ ‚Üí X ‚Üí M} :
    (LocallyFinite fun i ‚Ü¶ mulSupport <| f i) ‚Üî LocallyFinite fun i ‚Ü¶ mulTSupport <| f i :=
  ‚ü®LocallyFinite.closure, fun H ‚Ü¶ H.subset fun _ ‚Ü¶ subset_closure‚ü©

theorem LocallyFinite.smul_left [Zero R] [Zero M] [SMulWithZero R M]
    {s : Œπ ‚Üí X ‚Üí R} (h : LocallyFinite fun i ‚Ü¶ support <| s i) (f : Œπ ‚Üí X ‚Üí M) :
    LocallyFinite fun i ‚Ü¶ support <| s i ‚Ä¢ f i :=
  h.subset fun i x ‚Ü¶ mt <| fun h ‚Ü¶ by rw [Pi.smul_apply', h, zero_smul]

theorem LocallyFinite.smul_right [Zero M] [SMulZeroClass R M]
    {f : Œπ ‚Üí X ‚Üí M} (h : LocallyFinite fun i ‚Ü¶ support <| f i) (s : Œπ ‚Üí X ‚Üí R) :
    LocallyFinite fun i ‚Ü¶ support <| s i ‚Ä¢ f i :=
  h.subset fun i x ‚Ü¶ mt <| fun h ‚Ü¶ by rw [Pi.smul_apply', h, smul_zero]

end LocallyFinite

section Homeomorph

variable {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]

@[to_additive]
theorem HasCompactMulSupport.comp_homeomorph {M} [One M] {f : Y ‚Üí M}
    (hf : HasCompactMulSupport f) (œÜ : X ‚âÉ‚Çú Y) : HasCompactMulSupport (f ‚àò œÜ) :=
  hf.comp_isClosedEmbedding œÜ.isClosedEmbedding

end Homeomorph
