/-
Copyright (c) 2025 Yakov Pechersky. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yakov Pechersky
-/
import Mathlib.Topology.Algebra.LinearTopology
import Mathlib.Topology.Algebra.Valued.ValuedField

/-!
# Valuation rings of valued fields have a linear topology

## Main Results
* `IsLinearTopology.of_valued`: for a valued field `K`,
  the valuation ring `ğ’ª[K]` has a linear topology

-/
open Valued Filter Topology

variable {K Î“â‚€ : Type*} [Field K] [LinearOrderedCommGroupWithZero Î“â‚€] [Valued K Î“â‚€]

instance IsLinearTopology.of_valued :
    IsLinearTopology ğ’ª[K] K := by
  -- a linear topology in this case means a basis of submodules, which our valued field has
  -- thanks to `Valued.hasBasis_nhds_zero`
  -- later, we will need to deal with the case of the trivial submodule,
  -- which is open iff the topology is discrete, so we will claim that the topology is not discrete
  -- because the discrete topology case is trivially `IsLinearTopology`
  by_cases hd : DiscreteTopology K
  Â· infer_instance
  rw [isLinearTopology_iff_hasBasis_open_submodule]
  simp_rw [hasBasis_iff, mem_nhds_zero]
  intro t
  constructor
  Â· rintro âŸ¨r, hrâŸ©
    -- easy implication: given a `r : Î“â‚€Ë£`, we have an open submodule that is an open ball
    have : (algebraMap ğ’ª[K] K : ğ’ª[K] â†’ K) = Subtype.val := rfl
    -- this is `algebraMap (Ideal K) (Submodule ğ’ª[K] K)` but does not have lemmas about it
    refine âŸ¨Submodule.map (Algebra.linearMap ğ’ª[K] K) (idealBall K r), ?_, ?_âŸ©
    Â· simp only [Submodule.map_coe, Algebra.linearMap_apply, coe_idealBall]
      rw [â† (isOpenEmbedding_algebraMap_integer _).isOpen_iff_image_isOpen]
      exact isOpen_idealBall K r
    Â· refine hr.trans' ?_
      intro
      simp +contextual [this]
  Â· rintro âŸ¨I, hI, hItâŸ©
   -- harder implication: given an open submodule `I`, find an open ball in it.
   -- We need to get some `r : Î“â‚€Ë£` (really, `r â‰  0`).
   -- We will construct such an `r` from a nontrivial element of `I`,
   -- since `I` must not be trivial itself.
   -- If it was trivial, then it could not have been open, since then a singleton would be open,
   -- which contradicts the assumption that the topology is not discrete.
    have hI' : I â‰  âŠ¥ := by
      rintro rfl
      have : DiscreteTopology K := by
        rw [discreteTopology_iff_singleton_mem_nhds]
        intro y
        simpa using singleton_add_mem_nhds_of_nhds_zero y (hI.mem_nhds rfl)
      contradiction
    -- We now extract a nontrivial `x`, which will have a nonzero valuation `r := v x`,
    -- which we lift into the units `Î“â‚€Ë£`. The submodule ball associated to that `r` is necessarily
    -- contained in `I`.
    obtain âŸ¨x, hx, hx0âŸ© : âˆƒ y âˆˆ I, y â‰  0 := Submodule.exists_mem_ne_zero_of_ne_bot hI'
    replace hx0 : Valued.v (x : K) â‰  0 := by simp [hx0]
    refine âŸ¨Units.mk0 _ hx0, hIt.trans' ?_âŸ©
    exact submoduleBall_v_le_of_mem hx hx0

instance IsLinearTopology.of_valued' :
    IsLinearTopology ğ’ª[K] ğ’ª[K] := by
  have : IsLinearTopology ğ’ª[K] K := inferInstance
  rw [isLinearTopology_iff_hasBasis_open_submodule] at this
  have := this.comap (Subtype.val : ğ’ª[K] â†’ K)
  -- we need to convert the comap-ed neighborhood of zero of the field to the neighborhood of zero
  -- of the valuation ring,
  have hn0 : ğ“ (0 : ğ’ª[K]) = comap Subtype.val (ğ“ 0) := nhds_induced Subtype.val 0
  rw [â† hn0] at this
  rw [isLinearTopology_iff_hasBasis_open_submodule]
  refine this.to_hasBasis ?_ ?_
  Â· intro I hI
    exact âŸ¨I.comap (Algebra.linearMap ğ’ª[K] K), continuous_subtype_val.isOpen_preimage _ hI,
      subset_refl _âŸ©
  Â· intro I hI
    refine âŸ¨Submodule.map (Algebra.linearMap ğ’ª[K] K) I, ?_,
      (Set.preimage_image_eq _ (Subtype.val_injective)).leâŸ©
    simp only [Submodule.map_coe, Algebra.linearMap_apply]
    rwa [â† (isOpenEmbedding_algebraMap_integer _).isOpen_iff_image_isOpen]
