/-
Copyright (c) 2025 Yakov Pechersky. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yakov Pechersky
-/
import Mathlib.Topology.Algebra.LinearTopology
import Mathlib.Topology.Algebra.Valued.ValuedField

/-!
# Valuation rings of valued fields have a linear topology

## Main Results
* `IsLinearTopology.of_valued`: for a valued field `K`,
  the valuation ring `ğ’ª[K]` has a linear topology

-/
open Valued Filter Topology

variable {K Î“â‚€ : Type*} [Field K] [LinearOrderedCommGroupWithZero Î“â‚€] [Valued K Î“â‚€]

instance IsLinearTopology.of_valued :
    IsLinearTopology ğ’ª[K] K := by
  rw [isLinearTopology_iff_hasBasis_open_submodule]
  apply (hasBasis_nhds_zero K Î“â‚€).to_hasBasis
  Â· exact fun r _ â†¦ âŸ¨ltSubmodule K r, isOpen_ltSubmodule _ _, subset_rflâŸ©
  Â· intro I hI
    simpa [mem_nhds_zero] using hI.mem_nhds I.zero_mem

instance IsLinearTopology.of_valued' :
    IsLinearTopology ğ’ª[K] ğ’ª[K] := by
  have : IsLinearTopology ğ’ª[K] K := inferInstance
  rw [isLinearTopology_iff_hasBasis_open_submodule] at this
  have := this.comap (Subtype.val : ğ’ª[K] â†’ K)
  -- we need to convert the comap-ed neighborhood of zero of the field to the neighborhood of zero
  -- of the valuation ring,
  have hn0 : ğ“ (0 : ğ’ª[K]) = comap Subtype.val (ğ“ 0) := nhds_induced Subtype.val 0
  rw [â† hn0] at this
  rw [isLinearTopology_iff_hasBasis_open_submodule]
  refine this.to_hasBasis ?_ ?_
  Â· intro I hI
    exact âŸ¨I.comap (Algebra.linearMap ğ’ª[K] K), continuous_subtype_val.isOpen_preimage _ hI,
      subset_refl _âŸ©
  Â· intro I hI
    refine âŸ¨Submodule.map (Algebra.linearMap ğ’ª[K] K) I, ?_,
      (Set.preimage_image_eq _ (Subtype.val_injective)).leâŸ©
    simp only [Submodule.map_coe, Algebra.linearMap_apply]
    rwa [â† (isOpenEmbedding_algebraMap_integer _).isOpen_iff_image_isOpen]
