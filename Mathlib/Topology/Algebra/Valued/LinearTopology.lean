/-
Copyright (c) 2025 Yakov Pechersky. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yakov Pechersky
-/
import Mathlib.RingTheory.DiscreteValuationRing.TFAE
import Mathlib.Topology.Algebra.LinearTopology
import Mathlib.Topology.Algebra.Valued.ValuedField

/-!
# Valuation rings of valued fields have a linear topology

## Main Results
* `IsLinearTopology.of_isDiscreteValuationRing`: for a valued field `K`,
  the valuation ring `ğ’ª[K]` has a linear topology, when the valuation ring is a DVR.

-/
open Valued Filter Topology

variable {K Î“â‚€ : Type*} [Field K] [LinearOrderedCommGroupWithZero Î“â‚€]
    [Valued K Î“â‚€]

lemma Valued.maximalIdeal_eq_ball :
    (ğ“‚[K] : Set ğ’ª[K]) = {x : ğ’ª[K] | Valued.v (x : K) < 1} := by
  ext
  simp [Valuation.Integer.not_isUnit_iff_valuation_lt_one]

lemma _root_.Irreducible.maximalIdeal_pow_succ_eq_ball_pow [IsDiscreteValuationRing ğ’ª[K]]
    {Ï– : ğ’ª[K]} (h : Irreducible Ï–) (n : â„•) :
    ((ğ“‚[K] ^ (n + 1) : Ideal ğ’ª[K]) : Set ğ’ª[K]) =
      {x : ğ’ª[K] | Valued.v (x : K) < (Valued.v (Ï– : K)) ^ n} := by
  ext x
  simp only [h.maximalIdeal_eq, Ideal.span_singleton_pow, SetLike.mem_coe,
    Ideal.mem_span_singleton, â† map_pow, Set.mem_setOf_eq]
  constructor
  Â· rintro âŸ¨c, rflâŸ©
    simp only [pow_succ, mul_assoc, Subring.coe_mul, SubmonoidClass.coe_pow, map_mul, map_pow]
    have : Valued.v (Ï– : K) * Valued.v (c : K) < 1 := by
      refine mul_lt_one_of_nonneg_of_lt_one_left zero_le' ?_ c.prop
      simp [â† Valuation.Integer.not_isUnit_iff_valuation_lt_one, h.not_isUnit]
    simpa using mul_lt_mul_of_le_of_lt_of_nonneg_of_pos (le_refl _) this
      zero_le' (by simp [â† map_pow, Valued.v.pos_iff, h.ne_zero])
  Â· rcases eq_or_ne x 0 with rfl|hx
    Â· simp
    obtain âŸ¨k, u, rflâŸ© := IsDiscreteValuationRing.eq_unit_mul_pow_irreducible hx h
    have : Valued.v (u : K) = 1 := (Valuation.integer.integers Valued.v).valuation_unit u
    simp only [Subring.coe_mul, SubmonoidClass.coe_pow, map_mul, this, map_pow, one_mul,
      Units.isUnit, IsUnit.dvd_mul_left]
    intro H
    rw [pow_lt_pow_iff_right_of_lt_oneâ‚€ ] at H
    Â· exact pow_dvd_pow Ï– H
    Â· simp [Valued.v.pos_iff, h.ne_zero]
    Â· simp [â† Valuation.Integer.not_isUnit_iff_valuation_lt_one, h.not_isUnit]

lemma Valued.v_eq_one_iff_of_discreteTopology
    [MulArchimedean Î“â‚€] [DiscreteTopology K] {x : K} :
    Valued.v x = 1 â†” x â‰  0 := by
  have : ({0} : Set K) âˆˆ ğ“ 0 := by simp
  rw [mem_nhds_zero] at this
  obtain âŸ¨y, hyâŸ© := this
  simp only [Set.subset_singleton_iff, Set.mem_setOf_eq] at hy
  rcases lt_or_le (1 : Î“â‚€) y with h1 | h1
  Â· specialize hy 1
    simp [h1.not_le] at hy
  rw [â† Valued.v.pos_iff]
  constructor <;> intro h
  Â· simp [h]
  wlog hx1 : 1 < (Valued.v x) generalizing x y
  Â· push_neg at hx1
    rcases hx1.eq_or_lt with hx1 | hx1
    Â· exact hx1
    have hx0 : 0 < Valued.v xâ»Â¹ := by simp [h]
    specialize @this xâ»Â¹ y hy h1 hx0 ?_
    Â· simp [one_lt_invâ‚€ h, hx1]
    simpa using this
  obtain âŸ¨n, hnâŸ© := MulArchimedean.arch (y : Î“â‚€)â»Â¹ hx1
  rw [Valued.v.pos_iff] at h
  have : v (x ^ (n + 1))â»Â¹ < y := by
    refine (inv_le_of_inv_leâ‚€ (by simp) hn).trans_lt' ?_
    simp only [map_invâ‚€, map_pow]
    rw [inv_lt_invâ‚€]
    Â· exact pow_lt_pow_rightâ‚€ hx1 (by simp)
    Â· simp [â† map_pow, Valued.v.pos_iff, h]
    Â· simp [â† map_pow, Valued.v.pos_iff, h]
  simpa [h] using hy _ this

instance IsLinearTopology.of_isDiscreteValuationRing {K Î“â‚€ : Type*} [Field K]
    [LinearOrderedCommGroupWithZero Î“â‚€]
    [MulArchimedean Î“â‚€]
    [Valued K Î“â‚€]
    [IsDiscreteValuationRing ğ’ª[K]] :
    IsLinearTopology ğ’ª[K] ğ’ª[K] := by
  rw [isLinearTopology_iff_hasBasis_ideal, hasBasis_iff]
  intro U
  have : U âˆˆ ğ“ 0 â†” (Subtype.val '' U) âˆˆ ğ“ 0 := by
    rw [â† image_mem_map_iff Subtype.val_injective, map_nhds_subtype_coe_eq_nhds]
    Â· simp
    Â· rw [eventually_nhds_iff]
      exact âŸ¨ğ’ª[K], by simp, isOpen_integer K, by simp [Valuation.mem_integer_iff]âŸ©
  rw [this, Valued.mem_nhds_zero]
  obtain âŸ¨p, hpâŸ© := IsDiscreteValuationRing.exists_irreducible ğ’ª[K]
  have hp0 {n} : 0 < Valued.v (p : K) ^ n := by
    rw [â† map_pow, Valued.v.pos_iff]
    simp [hp.ne_zero]
  constructor
  Â· intro âŸ¨x, hxâŸ©
    rw [Set.subset_image_iff] at hx
    obtain âŸ¨V, hVU, hVâŸ© := hx
    obtain âŸ¨n, hnâŸ© : âˆƒ n : â„•, Valued.v (p : K) ^ n < x := by
      suffices âˆƒ n : â„•, (x : Î“â‚€)â»Â¹ â‰¤ (Valued.v (p : K) ^ n)â»Â¹ by
        obtain âŸ¨n, hnâŸ© := this
        refine âŸ¨n + 1, ?_âŸ©
        rw [inv_le_invâ‚€] at hn
        Â· refine hn.trans_lt' ?_
          refine pow_lt_pow_right_of_lt_oneâ‚€ ?_ ?_ ?_
          Â· simpa using hp0 (n := 1)
          Â· exact Valuation.Integer.not_isUnit_iff_valuation_lt_one.mp hp.not_isUnit
          Â· simp
        Â· simp
        Â· exact hp0
      simp_rw [â† inv_pow]
      apply MulArchimedean.arch (x : Î“â‚€)â»Â¹
      rw [one_lt_invâ‚€]
      Â· exact Valuation.Integer.not_isUnit_iff_valuation_lt_one.mp hp.not_isUnit
      Â· simpa using hp0 (n := 1)
    replace hV : V = {y : ğ’ª[K] | Valued.v (y : K) < x} := by
      ext âŸ¨y, hyâŸ©
      simp only [Set.mem_setOf_eq]
      constructor <;> intro h
      Â· exact hV.le âŸ¨âŸ¨y, hyâŸ©, h, rflâŸ©
      Â· obtain âŸ¨z, hz, rflâŸ© := hV.ge h
        simp [hz]
    replace hV : {y : ğ’ª[K] | Valued.v (y : K) < Valued.v (p : K) ^ n} âŠ† U := by
      refine subset_trans (subset_trans ?_ hV.ge) hVU
      rw [Set.setOf_subset_setOf]
      intro
      exact hn.trans'
    rw [â† hp.maximalIdeal_pow_succ_eq_ball_pow] at hV
    Â· refine âŸ¨_, ?_, hVâŸ©
      rw [hp.maximalIdeal_pow_succ_eq_ball_pow]
      refine IsOpen.mem_nhds ?_ ?_
      Â· simpa using continuous_subtype_val.isOpen_preimage _ (isOpen_ball _ _)
      Â· simp [hp0]
  Â· rintro âŸ¨V, hV, hV'âŸ©
    rcases eq_or_ne V âŠ¥ with hv0|hV0
    Â· -- contradiction, the bottom ideal is not open in the valuation ring of a valued field
      simp only [hv0, Submodule.bot_coe] at hV
      rw [mem_nhds_iff] at hV
      obtain âŸ¨s, hs, hs', hs0âŸ© := hV
      simp only [hv0, Submodule.bot_coe, Set.subset_singleton_iff] at hs
      replace hs : s = {0} := le_antisymm hs (by simpa using hs0)
      have : IsOpen ({0} : Set ğ’ª[K]) := by rwa [â† hs]
      replace this : IsOpen ({0} : Set K) := by
        simpa using (isOpen_integer _).isOpenMap_subtype_val _ this
      have _ := discreteTopology_of_isOpen_singleton_zero this
      have hp1 : Valued.v (p : K) < 1 := by
        exact Valuation.Integer.not_isUnit_iff_valuation_lt_one.mp hp.not_isUnit
      simp [(Valued.v_eq_one_iff_of_discreteTopology (x := (p : K))).mpr
        (by simpa using hp.ne_zero)] at hp1
    obtain âŸ¨n, hnâŸ© : âˆƒ n : â„•, V = ğ“‚[K] ^ n := by
      refine exists_maximalIdeal_pow_eq_of_principal ğ’ª[K] ?_ _ hV0
      exact IsPrincipalIdealRing.principal _
    refine âŸ¨Units.mk0 (Valued.v (p : K) ^ n) hp0.ne', ?_âŸ©
    simp only [Units.val_mk0]
    rw [Set.subset_image_iff]
    refine âŸ¨((ğ“‚[K] ^ (n + 1) : Ideal ğ’ª[K]) : Set ğ’ª[K]), ?_, ?_âŸ©
    Â· exact subset_trans (Ideal.pow_le_pow_right (Nat.le_succ n)) (subset_trans hn.ge hV')
    Â· rw [hp.maximalIdeal_pow_succ_eq_ball_pow]
      ext
      simp only [Set.mem_image, Set.mem_setOf_eq, Subtype.exists, exists_and_left, exists_prop,
        exists_eq_right_right, and_iff_left_iff_imp]
      intro hx
      refine hx.le.trans ?_
      rw [â† map_pow, â† Subring.coe_pow]
      exact Subtype.prop (p ^ n : ğ’ª[K])
