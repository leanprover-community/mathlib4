/-
Copyright (c) 2025 Yakov Pechersky. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yakov Pechersky
-/
import Mathlib.Topology.Algebra.LinearTopology
import Mathlib.Topology.Algebra.Valued.ValuedField

/-!
# Valuation rings of valued fields have a linear topology

## Main Results
* `IsLinearTopology.of_valued`: for a valued field `K`,
  the valuation ring `ğ’ª[K]` has a linear topology

-/
open Valued Filter Topology

variable {K Î“â‚€ : Type*} [Field K] [LinearOrderedCommGroupWithZero Î“â‚€] [Valued K Î“â‚€]

instance IsLinearTopology.of_valued :
    IsLinearTopology ğ’ª[K] ğ’ª[K] := by
  by_cases hd : DiscreteTopology K
  Â· have : DiscreteTopology ğ’ª[K] := inferInstance
    infer_instance
  rw [isLinearTopology_iff_hasBasis_ideal]
  have := (hasBasis_nhds_zero K Î“â‚€).comap (Subtype.val : ğ’ª[K] â†’ K)
  have hn0 : ğ“ (0 : ğ’ª[K]) = comap Subtype.val (ğ“ 0) := nhds_induced Subtype.val 0
  rw [â† hn0] at this
  refine this.to_hasBasis ?_ ?_
  Â· intro r _
    refine âŸ¨idealBall _ r, (isOpen_idealBall _ r).mem_nhds <| zero_mem _, ?_âŸ©
    simp
  Â· intro I hI
    have hI' : I â‰  âŠ¥ := by
      rintro rfl
      simp only [Submodule.bot_coe] at hI
      have : DiscreteTopology ğ’ª[K] := by
        rw [discreteTopology_iff_singleton_mem_nhds]
        intro y
        simpa using singleton_add_mem_nhds_of_nhds_zero y hI
      rw [Valued.discreteTopology_valuationRing_iff_discreteTopology] at this
      contradiction
    obtain âŸ¨x, hx, hx0âŸ© : âˆƒ y âˆˆ I, y â‰  0 := Submodule.exists_mem_ne_zero_of_ne_bot hI'
    let y : Î“â‚€ := Valued.v (x : K)
    lift y to Î“â‚€Ë£ with y' hy'
    Â· simp [y, hx0]
    refine âŸ¨y', trivial, ?_âŸ©
    simp only [Set.preimage_setOf_eq, y, hy']
    intro a ha
    simp only [Set.mem_setOf_eq, y] at ha
    have hax : Valued.v ((a : K) / x) â‰¤ 1 := by
      simp only [map_divâ‚€, y]
      rw [div_le_oneâ‚€]
      Â· exact ha.le
      Â· simp [Valued.v.pos_iff, hx0]
    have : a = x * âŸ¨_, haxâŸ© := by
      ext
      simp only [Subring.coe_mul, y]
      rw [mul_div_cancelâ‚€]
      simpa using hx0
    rw [this, SetLike.mem_coe]
    exact Ideal.IsTwoSided.mul_mem_of_left _ hx
