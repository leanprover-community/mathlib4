/-
Copyright (c) 2021 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
import Mathlib.Algebra.Order.Group.Units
import Mathlib.Topology.Algebra.Nonarchimedean.Bases
import Mathlib.Topology.Algebra.UniformFilterBasis
import Mathlib.RingTheory.Valuation.ValuationSubring

/-!
# The topology on a valued ring

In this file, we define the non archimedean topology induced by a valuation on a ring.
The main definition is a `Valued` type class which equips a ring with a valuation taking
values in a group with zero. Other instances are then deduced from this.
-/

open scoped Topology uniformity
open Set Valuation

noncomputable section

universe v u

variable {R : Type u} [Ring R] {Œì‚ÇÄ : Type v} [LinearOrderedCommGroupWithZero Œì‚ÇÄ]

namespace Valuation

variable (v : Valuation R Œì‚ÇÄ)

instance {M : Type*} [Monoid M] {r : M ‚Üí M ‚Üí Prop}
    [CovariantClass M M (¬∑ * ¬∑) r] (N : Submonoid M):
      CovariantClass N N (¬∑ * ¬∑) (fun x y ‚Ü¶ r x y) := ‚ü® by
  rintro ‚ü®m, _‚ü© ‚ü®n‚ÇÅ, _‚ü© ‚ü®n‚ÇÇ, _‚ü©
  simpa only [Submonoid.mk_mul_mk] using CovariantClass.elim m ‚ü©

instance {M : Type*} [Monoid M] [LT M]
    [CovariantClass M M (¬∑ * ¬∑) (¬∑ < ¬∑)] (N : Submonoid M):
      CovariantClass N N (¬∑ * ¬∑) (fun x y ‚Ü¶ x < y) := ‚ü® by
  rintro ‚ü®m, _‚ü© ‚ü®n‚ÇÅ, _‚ü© ‚ü®n‚ÇÇ, _‚ü©
  simp only [Subtype.mk_lt_mk, Submonoid.mk_mul_mk]
  exact fun a ‚Ü¶ mul_lt_mul_left' a m
  ‚ü©

lemma units_min_eq (Œ≥‚ÇÄ Œ≥‚ÇÅ : v.rangeGroup‚ÇÄÀ£) :
    (min Œ≥‚ÇÄ Œ≥‚ÇÅ).map v.rangeGroup‚ÇÄ.subtype =
      min (Œ≥‚ÇÄ.map v.rangeGroup‚ÇÄ.subtype) (Œ≥‚ÇÅ.map v.rangeGroup‚ÇÄ.subtype) := by
  simp [Units.ext_iff]

lemma units_max_eq (Œ≥‚ÇÄ Œ≥‚ÇÅ : v.rangeGroup‚ÇÄÀ£) :
    (max Œ≥‚ÇÄ Œ≥‚ÇÅ).map v.rangeGroup‚ÇÄ.subtype =
      max (Œ≥‚ÇÄ.map v.rangeGroup‚ÇÄ.subtype) (Œ≥‚ÇÅ.map v.rangeGroup‚ÇÄ.subtype) := by
  simp [Units.ext_iff]

/-- The basis of open subgroups for the topology on a ring determined by a valuation. -/
theorem subgroups_basis :
    RingSubgroupsBasis fun Œ≥ : v.rangeGroup‚ÇÄÀ£ ‚Ü¶
      (v.ltAddSubgroup (Œ≥.map v.rangeGroup‚ÇÄ.subtype) : AddSubgroup R) :=
  { inter Œ≥‚ÇÄ Œ≥‚ÇÅ := by
      use min Œ≥‚ÇÄ Œ≥‚ÇÅ
      simp only [units_min_eq, ltAddSubgroup_min]
      apply le_of_eq
      congr
    mul Œ≥ := by
      obtain ‚ü®Œ≥‚ÇÄ, h‚ü© := exists_square_le Œ≥
      use Œ≥‚ÇÄ
      rintro - ‚ü®r, r_in, s, s_in, rfl‚ü©
      simp only [SetLike.mem_coe, mem_ltAddSubgroup_iff] at r_in s_in
      simp only [SetLike.mem_coe, mem_ltAddSubgroup_iff, _root_.map_mul]
      exact lt_of_lt_of_le ((mul_lt_mul'' r_in s_in) zero_le' zero_le') h
    leftMul x Œ≥ := by
      rcases GroupWithZero.eq_zero_or_unit (v x) with (Hx | ‚ü®Œ≥x, Hx‚ü©)
      ¬∑ use 1
        rintro y hy
        simp only [mem_preimage, SetLike.mem_coe, mem_ltAddSubgroup_iff, map_mul, Hx, zero_mul,
          Units.coe_map, Submonoid.subtype_apply]
        apply Œ≥.zero_lt
      ¬∑ have Hx' : IsUnit (‚ü®v x, mem_rangeGroup‚ÇÄ v‚ü© : v.rangeGroup‚ÇÄ) := by
          simp [isUnit_iff_ne_zero, ne_eq, ‚Üê Subtype.coe_inj, Hx]
        use Hx'.unit‚Åª¬π * Œ≥
        intro y vy_lt
        simp only [mem_preimage, SetLike.mem_coe, mem_ltAddSubgroup_iff, _root_.map_mul, Hx]
        simpa [mem_ltAddSubgroup_iff, mul_comm, Hx, lt_mul_inv_iff‚ÇÄ' Œ≥x.zero_lt] using vy_lt
    rightMul x Œ≥ := by
      rcases GroupWithZero.eq_zero_or_unit (v x) with (Hx | ‚ü®Œ≥x, Hx‚ü©)
      ¬∑ use 1
        rintro y _
        simp only [mem_preimage, SetLike.mem_coe, mem_ltAddSubgroup_iff]
        rw [Valuation.map_mul, Hx, mul_zero]
        exact Units.zero_lt _
      ¬∑ have Hx' : IsUnit (‚ü®v x, mem_rangeGroup‚ÇÄ v‚ü© : v.rangeGroup‚ÇÄ) := by
          simp [isUnit_iff_ne_zero, ne_eq, ‚Üê Subtype.coe_inj, Hx]
        use Hx'.unit‚Åª¬π * Œ≥
        rintro y vy_lt
        simp only [mem_preimage, SetLike.mem_coe, mem_ltAddSubgroup_iff, _root_.map_mul, Hx]
        simpa [mem_ltAddSubgroup_iff, mul_comm, Hx, lt_mul_inv_iff‚ÇÄ' Œ≥x.zero_lt] using vy_lt }

end Valuation


/-- A valued ring is a ring that comes equipped with a distinguished group-valued valuation.
The class `Valued` is designed for the situation that there is a canonical valuation on the ring.

TODO: show that there always exists an equivalent valuation taking values in a type belonging to
the same universe as the ring.

See Note [forgetful inheritance] for why we extend `UniformSpace`, `IsUniformAddGroup`. -/
class Valued (R : Type u) [Ring R] (Œì‚ÇÄ : outParam (Type v))
  [LinearOrderedCommGroupWithZero Œì‚ÇÄ] extends UniformSpace R, IsUniformAddGroup R where
  v : Valuation R Œì‚ÇÄ
  is_topological_valuation : ‚àÄ s, s ‚àà ùìù (0 : R) ‚Üî
    ‚àÉ Œ≥ ‚àà v.rangeGroup‚ÇÄ, Œ≥ ‚â† 0 ‚àß { x : R | v x < Œ≥ } ‚äÜ s

namespace Valued

lemma exists_iff_exists (v : Valuation R Œì‚ÇÄ) (motive : Œì‚ÇÄ ‚Üí Prop) :
    (‚àÉ u : v.rangeGroup‚ÇÄÀ£, motive u) ‚Üî
      ‚àÉ Œ≥ ‚àà v.rangeGroup‚ÇÄ, Œ≥ ‚â† 0 ‚àß motive Œ≥ := by
  constructor
  ¬∑ rintro ‚ü®u, h‚ü©
    refine ‚ü®u.val, u.val.prop, fun h' ‚Ü¶ ?_, h‚ü©
    apply u.ne_zero
    rw [‚Üê Subtype.coe_inj, h', MonoidHomWithZero.range‚ÇÄ_coe_zero]
  ¬∑ rintro ‚ü®Œ≥, hŒ≥, h, h'‚ü©
    have hŒ≥' : IsUnit (‚ü®Œ≥, hŒ≥‚ü© : v.rangeGroup‚ÇÄ) := by
      simp [‚Üê Subtype.coe_inj, h]
    exact ‚ü®hŒ≥'.unit, h'‚ü©

/-- Alternative `Valued` constructor for use when there is no preferred `UniformSpace` structure. -/
def mk' (v : Valuation R Œì‚ÇÄ) : Valued R Œì‚ÇÄ :=
  { v
    toUniformSpace := @IsTopologicalAddGroup.toUniformSpace R _ v.subgroups_basis.topology _
    toIsUniformAddGroup := @isUniformAddGroup_of_addCommGroup _ _ v.subgroups_basis.topology _
    is_topological_valuation := by
      letI := @IsTopologicalAddGroup.toUniformSpace R _ v.subgroups_basis.topology _
      intro s
      rw [Filter.hasBasis_iff.mp v.subgroups_basis.hasBasis_nhds_zero s]
      simp only [true_and, Subtype.exists, exists_prop]
      exact exists_iff_exists v (fun Œ≥ ‚Ü¶ {x | v x < Œ≥} ‚äÜ s) }

variable (R Œì‚ÇÄ)
variable [_i : Valued R Œì‚ÇÄ]

theorem hasBasis_nhds_zero :
    (ùìù (0 : R)).HasBasis (fun _ => True) fun Œ≥ : _i.v.rangeGroup‚ÇÄÀ£ ‚Ü¶ { x | v x < Œ≥ } := by
  simp [Filter.hasBasis_iff, is_topological_valuation]
  intro s
  rw [iff_comm]
  apply exists_iff_exists _i.v (fun Œ≥ ‚Ü¶ {x | v x < Œ≥} ‚äÜ s)

open Uniformity in
theorem hasBasis_uniformity : (ùì§ R).HasBasis (fun _ => True)
    fun Œ≥ : _i.v.rangeGroup‚ÇÄÀ£ ‚Ü¶
      { p : R √ó R | v (p.2 - p.1) < Œ≥.map v.rangeGroup‚ÇÄ.subtype } := by
  rw [uniformity_eq_comap_nhds_zero]
  exact (hasBasis_nhds_zero R Œì‚ÇÄ).comap _

theorem toUniformSpace_eq :
    toUniformSpace = @IsTopologicalAddGroup.toUniformSpace R _ v.subgroups_basis.topology _ :=
  UniformSpace.ext
    ((hasBasis_uniformity R Œì‚ÇÄ).eq_of_same_basis <| v.subgroups_basis.hasBasis_nhds_zero.comap _)

variable {R Œì‚ÇÄ}

theorem mem_nhds {s : Set R} {x : R} : s ‚àà ùìù x ‚Üî ‚àÉ Œ≥ : _i.v.rangeGroup‚ÇÄÀ£,
    { y | v (y - x) < Œ≥.map v.rangeGroup‚ÇÄ.subtype } ‚äÜ s := by
  simp [‚Üê nhds_translation_add_neg x, ‚Üê sub_eq_add_neg, preimage_setOf_eq, true_and,
    ((hasBasis_nhds_zero R Œì‚ÇÄ).comap fun y => y - x).mem_iff]

theorem mem_nhds_zero {s : Set R} : s ‚àà ùìù (0 : R) ‚Üî
    ‚àÉ Œ≥ : _i.v.rangeGroup‚ÇÄÀ£, { x | _i.v x < Œ≥.map v.rangeGroup‚ÇÄ.subtype } ‚äÜ s := by
  simp only [mem_nhds, sub_zero]

theorem loc_const {x : R} (h : (v x : Œì‚ÇÄ) ‚â† 0) : { y : R | v y = v x } ‚àà ùìù x := by
  rw [mem_nhds]
  have hx : IsUnit (‚ü®v x, v.mem_rangeGroup‚ÇÄ‚ü© : v.rangeGroup‚ÇÄ) := by
    simp [‚Üê Subtype.coe_inj, h]
  use hx.unit
  intro y
  exact Valuation.map_eq_of_sub_lt _

/-- A valued ring is a topological ring -/
instance (priority := 100) : IsTopologicalRing R :=
  (toUniformSpace_eq R Œì‚ÇÄ).symm ‚ñ∏ v.subgroups_basis.toRingFilterBasis.isTopologicalRing

theorem cauchy_iff {F : Filter R} : Cauchy F ‚Üî
    F.NeBot ‚àß ‚àÄ Œ≥ : _i.v.rangeGroup‚ÇÄÀ£, ‚àÉ M ‚àà F, ‚àÄ·µâ (x ‚àà M) (y ‚àà M),
      v (y - x) < Œ≥.map v.rangeGroup‚ÇÄ.subtype := by
  rw [toUniformSpace_eq, AddGroupFilterBasis.cauchy_iff]
  apply and_congr Iff.rfl
  simp_rw [Valued.v.subgroups_basis.mem_addGroupFilterBasis_iff]
  constructor
  ¬∑ intro h Œ≥
    exact h _ (Valued.v.subgroups_basis.mem_addGroupFilterBasis _)
  ¬∑ rintro h - ‚ü®Œ≥, rfl‚ü©
    exact h Œ≥

variable (R)

theorem ball_subset_ball {a b : R} {r s : Œì‚ÇÄ}
    (hrs : r ‚â§ s) (hab : v (a - b) < s) :
    { x | v (x - a) < r } ‚äÜ { x | v (x - b) < s } := by
  intro x
  simp only [mem_setOf_eq]
  intro hx
  rw [‚Üê sub_add_sub_cancel x a b]
  apply map_add_lt _ (lt_of_lt_of_le hx hrs) hab

/-- An open ball in a valued ring is open if its radius is a unit of `rangeGroup‚ÇÄ` -/
theorem isOpen_ball (a : R) (r : _i.v.rangeGroup‚ÇÄÀ£) :
    IsOpen (X := R) {x | v (x - a) < r } := by
  rw [isOpen_iff_mem_nhds]
  intro x hx
  rw [mem_nhds]
  simp only [setOf_subset_setOf]
  simp only [mem_setOf_eq] at hx
  use r
  intro y hy
  simp only [Units.coe_map, Submonoid.subtype_apply] at hy
  rw [‚Üê sub_add_sub_cancel y x a]
  apply v.map_add_lt hy hx

/-- An open ball centered at the origin in a valued ring is closed. -/
theorem isClosed_ball (a : R) (r : Œì‚ÇÄ) :
    IsClosed (X := R) {x | v (x - a) < r} := by
  rcases eq_or_ne r 0 with rfl|hr
  ¬∑ simp
  rw [‚Üê isOpen_compl_iff, isOpen_iff_mem_nhds]
  intro b hb
  simp only [mem_compl_iff, mem_setOf_eq, not_lt] at hb
  simp only [mem_nhds_iff]
  use {x | v (x - b) < v (b - a) }
  constructor
  ¬∑ intro x hx
    simp only [mem_setOf_eq, mem_compl_iff, not_lt] at ‚ä¢ hx
    rwa [‚Üê sub_add_sub_cancel x b a, map_add_eq_of_lt_right v hx]
  constructor
  ¬∑ have ha' : IsUnit (‚ü®v (b - a) , v.mem_rangeGroup‚ÇÄ‚ü© : v.rangeGroup‚ÇÄ) := by
      simp only [isUnit_iff_ne_zero, ne_eq, ‚Üê Subtype.coe_inj, MonoidHomWithZero.range‚ÇÄ_coe_zero]
      intro h
      apply hr
      rwa [h, le_zero_iff] at hb
    exact isOpen_ball R b ha'.unit
  ¬∑ simp only [mem_setOf_eq, sub_self, map_zero]
    exact lt_of_lt_of_le (zero_lt_iff.mpr hr) hb

/-- In a valued ring, an open ball whose radius is unit in `rangeGroup‚ÇÄ` is clopen. -/
theorem isClopen_ball (a : R) (r : _i.v.rangeGroup‚ÇÄÀ£) :
    IsClopen (X := R) {x | v (x - a) < r } :=
  ‚ü®isClosed_ball _ _ _, isOpen_ball _ _ _‚ü©

/-- In a valued ring, a closed ball whose radius is a unit of `rangeGroup‚ÇÄ` is open. -/
theorem isOpen_closedball {a : R} {r : _i.v.rangeGroup‚ÇÄÀ£} :
    IsOpen (X := R) {x | v (x - a) ‚â§ r} := by
  rw [isOpen_iff_mem_nhds]
  intro x hx
  rw [mem_nhds]
  simp only [setOf_subset_setOf]
  use r
  intro b hb
  simp only [mem_setOf_eq] at hx
  simp only [Units.coe_map, Submonoid.subtype_apply] at hb
  rw [‚Üê sub_add_sub_cancel b x a]
  exact v.map_add_le (le_of_lt hb) hx

/-- A closed ball in a valued ring is closed. -/
theorem isClosed_closedBall (a : R) (r : Œì‚ÇÄ) :
    IsClosed (X := R) {x | v (x - a) ‚â§ r} := by
  rw [‚Üê isOpen_compl_iff, isOpen_iff_mem_nhds]
  intro x hx
  rw [mem_nhds]
  simp only [mem_compl_iff, mem_setOf_eq, not_le] at hx
  have : IsUnit (‚ü®v (x - a), v.mem_rangeGroup‚ÇÄ‚ü© : v.rangeGroup‚ÇÄ) := by
    simp only [isUnit_iff_ne_zero, ne_eq]
    simp only [‚Üê Subtype.coe_inj, MonoidHomWithZero.range‚ÇÄ_coe_zero,
      ‚Üê zero_lt_iff]
    exact lt_of_le_of_lt zero_le' hx
  use this.unit
  intro y
  simp only [Units.coe_map, IsUnit.unit_spec, Submonoid.subtype_apply, mem_setOf_eq, mem_compl_iff,
    not_le]
  intro hy
  rw [‚Üê sub_add_sub_cancel y x a, add_comm, map_add_eq_of_lt_left v hy]
  exact hx

/-- In a valued ring, a closed ball whose radius is a unit of `v.rangeGroup‚ÇÄ` is clopen. -/
theorem isClopen_closedBall {a : R} {r : _i.v.rangeGroup‚ÇÄÀ£} :
    IsClopen (X := R) {x | v (x - a) ‚â§ r} :=
  ‚ü®isClosed_closedBall _ _ _, isOpen_closedball _‚ü©

/-- A sphere centred at the origin in a valued ring is clopen. -/
theorem isClopen_sphere {a : R} {r : Œì‚ÇÄ} (hr : r ‚â† 0) :
    IsClopen (X := R) {x | v (x - a) = r} := by
  by_cases h : ‚àÉ x, v (x - a) = r
  ¬∑ have H : {x : R | v (x - a) = r} = {x | v (x - a) ‚â§ r} \ {x | v (x - a) < r} := by
      ext x; simp [‚Üê le_antisymm_iff]
    rw [H]
    suffices ‚àÉ Œ≥ : _i.v.rangeGroup‚ÇÄÀ£, Œ≥ = r by
      obtain ‚ü®Œ≥, rfl‚ü© := this
      exact IsClopen.diff (isClopen_closedBall R) (isClopen_ball R a Œ≥)
    obtain ‚ü®x, h‚ü© := h
    have h' : IsUnit (‚ü®v (x-a), v.mem_rangeGroup‚ÇÄ‚ü© : v.rangeGroup‚ÇÄ) := by
      simpa [isUnit_iff_ne_zero, ne_eq, ‚Üê Subtype.coe_inj, h] using hr
    use h'.unit
    simp [‚Üê h]
  ¬∑ convert isClopen_empty
    apply Set.eq_empty_of_forall_not_mem
    push_neg at h
    exact h

/-- A sphere centred at the origin in a valued ring is open. -/
theorem isOpen_sphere {a : R} {r : Œì‚ÇÄ} (hr : r ‚â† 0) :
    IsOpen (X := R) {x | v (x - a) = r} :=
  isClopen_sphere _ hr |>.isOpen

/-- A sphere centred at the origin in a valued ring is closed. -/
theorem isClosed_sphere (a : R) (r : Œì‚ÇÄ) :
    IsClosed (X := R) {x | v (x - a) = r} := by
  rcases eq_or_ne r 0 with rfl|hr
  ¬∑ simpa using isClosed_closedBall R a 0
  exact isClopen_sphere _ hr |>.isClosed

/-- The closed unit ball in a valued ring is open. -/
theorem isOpen_integer : IsOpen (_i.v.integer : Set R) := by
  simpa only [sub_zero, Units.val_one, OneMemClass.coe_one] using
    isOpen_closedball R (a := 0) (r := 1)

@[deprecated (since := "2025-04-25")]
alias integer_isOpen := isOpen_integer

/-- The closed unit ball of a valued ring is closed. -/
theorem isClosed_integer : IsClosed (_i.v.integer : Set R) := by
  simpa only [sub_zero] using isClosed_closedBall R 0 1

/-- The closed unit ball of a valued ring is clopen. -/
theorem isClopen_integer : IsClopen (_i.v.integer : Set R) :=
  ‚ü®isClosed_integer _, isOpen_integer _‚ü©

/-- The valuation subring of a valued field is open. -/
theorem isOpen_valuationSubring (K : Type u) [Field K] [hv : Valued K Œì‚ÇÄ] :
    IsOpen (hv.v.valuationSubring : Set K) :=
  isOpen_integer K

@[deprecated (since := "2025-04-25")]
alias valuationSubring_isOpen := isOpen_valuationSubring

/-- The valuation subring of a valued field is closed. -/
theorem isClosed_valuationSubring (K : Type u) [Field K] [hv : Valued K Œì‚ÇÄ] :
    IsClosed (hv.v.valuationSubring : Set K) :=
  isClosed_integer K

/-- The valuation subring of a valued field is clopen. -/
theorem isClopen_valuationSubring (K : Type u) [Field K] [hv : Valued K Œì‚ÇÄ] :
    IsClopen (hv.v.valuationSubring : Set K) :=
  isClopen_integer K

end Valued
