/-
Copyright (c) 2021 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
import Mathlib.Algebra.Order.Group.Units
import Mathlib.Topology.Algebra.Nonarchimedean.Bases
import Mathlib.Topology.Algebra.UniformFilterBasis
import Mathlib.RingTheory.Valuation.ValuationSubring

/-!
# The topology on a valued ring

In this file, we define the non archimedean topology induced by a valuation on a ring.
The main definition is a `Valued` type class which equips a ring with a valuation taking
values in a group with zero. Other instances are then deduced from this.
-/

open scoped Topology uniformity
open Set Valuation

noncomputable section

universe v u

variable {R : Type u} [Ring R] {Œì‚ÇÄ : Type v} [LinearOrderedCommGroupWithZero Œì‚ÇÄ]

namespace Valuation

variable (v : Valuation R Œì‚ÇÄ)

/-- The basis of open subgroups for the topology on a ring determined by a valuation. -/
theorem subgroups_basis : RingSubgroupsBasis fun Œ≥ : Œì‚ÇÄÀ£ => (v.ltAddSubgroup Œ≥ : AddSubgroup R) :=
  { inter _ _ :=
      ‚ü®_, le_inf
        (ltAddSubgroup_mono _ (min_le_left _ _)) (ltAddSubgroup_mono _ (min_le_right _ _))‚ü©
    mul := by
      rintro Œ≥
      obtain ‚ü®Œ≥‚ÇÄ, h‚ü© := exists_square_le Œ≥
      use Œ≥‚ÇÄ
      rintro - ‚ü®r, r_in, s, s_in, rfl‚ü©
      simp only [SetLike.mem_coe, mem_ltAddSubgroup_iff] at r_in s_in
      calc
        (v (r * s) : Œì‚ÇÄ) = v r * v s := Valuation.map_mul _ _ _
        _ < Œ≥‚ÇÄ * Œ≥‚ÇÄ := by gcongr <;> exact zero_le'
        _ ‚â§ Œ≥ := mod_cast h
    leftMul := by
      rintro x Œ≥
      rcases GroupWithZero.eq_zero_or_unit (v x) with (Hx | ‚ü®Œ≥x, Hx‚ü©)
      ¬∑ use (1 : Œì‚ÇÄÀ£)
        rintro y
        simp [Hx]
      ¬∑ use Œ≥x‚Åª¬π * Œ≥
        simp [subset_def, lt_inv_mul_iff‚ÇÄ, Hx]
    rightMul := by
      rintro x Œ≥
      rcases GroupWithZero.eq_zero_or_unit (v x) with (Hx | ‚ü®Œ≥x, Hx‚ü©)
      ¬∑ use 1
        simp [subset_def, Hx]
      ¬∑ use Œ≥x‚Åª¬π * Œ≥
        simp [subset_def, lt_mul_inv_iff‚ÇÄ, Hx, mul_comm] }

end Valuation

/-- A valued ring is a ring that comes equipped with a distinguished valuation. The class `Valued`
is designed for the situation that there is a canonical valuation on the ring.

TODO: show that there always exists an equivalent valuation taking values in a type belonging to
the same universe as the ring.

See Note [forgetful inheritance] for why we extend `UniformSpace`, `IsUniformAddGroup`. -/
class Valued (R : Type u) [Ring R] (Œì‚ÇÄ : outParam (Type v))
  [LinearOrderedCommGroupWithZero Œì‚ÇÄ] extends UniformSpace R, IsUniformAddGroup R where
  v : Valuation R Œì‚ÇÄ
  is_topological_valuation : ‚àÄ s, s ‚àà ùìù (0 : R) ‚Üî ‚àÉ Œ≥ : Œì‚ÇÄÀ£, { x : R | v x < Œ≥ } ‚äÜ s

namespace Valued

/-- Alternative `Valued` constructor for use when there is no preferred `UniformSpace` structure. -/
def mk' (v : Valuation R Œì‚ÇÄ) : Valued R Œì‚ÇÄ :=
  { v
    toUniformSpace := @IsTopologicalAddGroup.toUniformSpace R _ v.subgroups_basis.topology _
    toIsUniformAddGroup := @isUniformAddGroup_of_addCommGroup _ _ v.subgroups_basis.topology _
    is_topological_valuation := by
      letI := @IsTopologicalAddGroup.toUniformSpace R _ v.subgroups_basis.topology _
      intro s
      rw [Filter.hasBasis_iff.mp v.subgroups_basis.hasBasis_nhds_zero s]
      exact exists_congr fun Œ≥ => by rw [true_and]; rfl }

variable (R Œì‚ÇÄ)
variable [_i : Valued R Œì‚ÇÄ]

theorem hasBasis_nhds_zero :
    (ùìù (0 : R)).HasBasis (fun _ => True) fun Œ≥ : Œì‚ÇÄÀ£ => { x | v x < (Œ≥ : Œì‚ÇÄ) } := by
  simp [Filter.hasBasis_iff, is_topological_valuation]

open Uniformity in
theorem hasBasis_uniformity : (ùì§ R).HasBasis (fun _ => True)
    fun Œ≥ : Œì‚ÇÄÀ£ => { p : R √ó R | v (p.2 - p.1) < (Œ≥ : Œì‚ÇÄ) } := by
  rw [uniformity_eq_comap_nhds_zero]
  exact (hasBasis_nhds_zero R Œì‚ÇÄ).comap _

theorem toUniformSpace_eq :
    toUniformSpace = @IsTopologicalAddGroup.toUniformSpace R _ v.subgroups_basis.topology _ :=
  UniformSpace.ext
    ((hasBasis_uniformity R Œì‚ÇÄ).eq_of_same_basis <| v.subgroups_basis.hasBasis_nhds_zero.comap _)

variable {R Œì‚ÇÄ}

theorem mem_nhds {s : Set R} {x : R} : s ‚àà ùìù x ‚Üî ‚àÉ Œ≥ : Œì‚ÇÄÀ£, { y | (v (y - x) : Œì‚ÇÄ) < Œ≥ } ‚äÜ s := by
  simp only [‚Üê nhds_translation_add_neg x, ‚Üê sub_eq_add_neg, preimage_setOf_eq, true_and,
    ((hasBasis_nhds_zero R Œì‚ÇÄ).comap fun y => y - x).mem_iff]

theorem mem_nhds_zero {s : Set R} : s ‚àà ùìù (0 : R) ‚Üî ‚àÉ Œ≥ : Œì‚ÇÄÀ£, { x | v x < (Œ≥ : Œì‚ÇÄ) } ‚äÜ s := by
  simp only [mem_nhds, sub_zero]

theorem loc_const {x : R} (h : (v x : Œì‚ÇÄ) ‚â† 0) : { y : R | v y = v x } ‚àà ùìù x := by
  rw [mem_nhds]
  use Units.mk0 _ h
  rw [Units.val_mk0]
  intro y y_in
  exact Valuation.map_eq_of_sub_lt _ y_in

instance (priority := 100) : IsTopologicalRing R :=
  (toUniformSpace_eq R Œì‚ÇÄ).symm ‚ñ∏ v.subgroups_basis.toRingFilterBasis.isTopologicalRing

theorem cauchy_iff {F : Filter R} : Cauchy F ‚Üî
    F.NeBot ‚àß ‚àÄ Œ≥ : Œì‚ÇÄÀ£, ‚àÉ M ‚àà F, ‚àÄ·µâ (x ‚àà M) (y ‚àà M), (v (y - x) : Œì‚ÇÄ) < Œ≥ := by
  rw [toUniformSpace_eq, AddGroupFilterBasis.cauchy_iff]
  apply and_congr Iff.rfl
  simp_rw [Valued.v.subgroups_basis.mem_addGroupFilterBasis_iff]
  constructor
  ¬∑ intro h Œ≥
    exact h _ (Valued.v.subgroups_basis.mem_addGroupFilterBasis _)
  ¬∑ rintro h - ‚ü®Œ≥, rfl‚ü©
    exact h Œ≥

variable (R)

/-- An open ball centred at the origin in a valued ring is open. -/
theorem isOpen_ball (r : Œì‚ÇÄ) : IsOpen (X := R) {x | v x < r} := by
  rw [isOpen_iff_mem_nhds]
  rcases eq_or_ne r 0 with rfl|hr
  ¬∑ simp
  intro x hx
  rw [mem_nhds]
  simp only [setOf_subset_setOf]
  exact ‚ü®Units.mk0 _ hr,
    fun y hy => (sub_add_cancel y x).symm ‚ñ∏ (v.map_add _ x).trans_lt (max_lt hy hx)‚ü©

/-- An open ball centred at the origin in a valued ring is closed. -/
theorem isClosed_ball (r : Œì‚ÇÄ) : IsClosed (X := R) {x | v x < r} := by
  rcases eq_or_ne r 0 with rfl|hr
  ¬∑ simp
  exact AddSubgroup.isClosed_of_isOpen
    (Valuation.ltAddSubgroup v (Units.mk0 r hr))
    (isOpen_ball _ _)

/-- An open ball centred at the origin in a valued ring is clopen. -/
theorem isClopen_ball (r : Œì‚ÇÄ) : IsClopen (X := R) {x | v x < r} :=
  ‚ü®isClosed_ball _ _, isOpen_ball _ _‚ü©

lemma isOpen_ltAddSubgroup (Œ≥ : Œì‚ÇÄÀ£) :
    IsOpen ((v.ltAddSubgroup Œ≥ : AddSubgroup R) : Set R) :=
  isOpen_ball _ _

lemma isClosed_ltAddSubgroup (Œ≥ : Œì‚ÇÄÀ£) :
    IsClosed ((v.ltAddSubgroup Œ≥ : AddSubgroup R) : Set R) :=
  isClosed_ball _ _

lemma isClopen_ltAddSubgroup (Œ≥ : Œì‚ÇÄÀ£) :
    IsClopen ((v.ltAddSubgroup Œ≥ : AddSubgroup R) : Set R) :=
  isClopen_ball _ _

/-- A closed ball centred at the origin in a valued ring is open. -/
theorem isOpen_closedBall {r : Œì‚ÇÄ} (hr : r ‚â† 0) : IsOpen (X := R) {x | v x ‚â§ r} := by
  rw [isOpen_iff_mem_nhds]
  intro x hx
  rw [mem_nhds]
  simp only [setOf_subset_setOf]
  exact ‚ü®Units.mk0 _ hr,
    fun y hy => (sub_add_cancel y x).symm ‚ñ∏ le_trans (v.map_add _ _) (max_le (le_of_lt hy) hx)‚ü©

@[deprecated (since := "2025-06-04")]
alias isOpen_closedball := isOpen_closedBall

/-- A closed ball centred at the origin in a valued ring is closed. -/
theorem isClosed_closedBall (r : Œì‚ÇÄ) : IsClosed (X := R) {x | v x ‚â§ r} := by
  rw [‚Üê isOpen_compl_iff, isOpen_iff_mem_nhds]
  intro x hx
  rw [mem_nhds]
  have hx' : v x ‚â† 0 := ne_of_gt <| lt_of_le_of_lt zero_le' <| lt_of_not_ge hx
  exact ‚ü®Units.mk0 _ hx', fun y hy hy' => ne_of_lt hy <| map_sub_swap v x y ‚ñ∏
      (Valuation.map_sub_eq_of_lt_left _ <| lt_of_le_of_lt hy' (lt_of_not_ge hx))‚ü©

/-- A closed ball centred at the origin in a valued ring is clopen. -/
theorem isClopen_closedBall {r : Œì‚ÇÄ} (hr : r ‚â† 0) : IsClopen (X := R) {x | v x ‚â§ r} :=
  ‚ü®isClosed_closedBall _ _, isOpen_closedBall _ hr‚ü©

lemma isOpen_leAddSubgroup {Œ≥ : Œì‚ÇÄ} (hŒ≥ : Œ≥ ‚â† 0) :
    IsOpen ((v.leAddSubgroup Œ≥ : AddSubgroup R) : Set R) :=
  isOpen_closedBall _ hŒ≥

lemma isClosed_leAddSubgroup (Œ≥ : Œì‚ÇÄ) :
    IsClosed ((v.leAddSubgroup Œ≥ : AddSubgroup R) : Set R) :=
  isClosed_closedBall _ _

lemma isClopen_leAddSubgroup {Œ≥ : Œì‚ÇÄ} (hŒ≥ : Œ≥ ‚â† 0) :
    IsClopen ((v.leAddSubgroup Œ≥ : AddSubgroup R) : Set R) :=
  isClopen_closedBall _ hŒ≥

/-- A sphere centred at the origin in a valued ring is clopen. -/
theorem isClopen_sphere {r : Œì‚ÇÄ} (hr : r ‚â† 0) : IsClopen (X := R) {x | v x = r} := by
  have h : {x : R | v x = r} = {x | v x ‚â§ r} \ {x | v x < r} := by
    ext x
    simp [‚Üê le_antisymm_iff]
  rw [h]
  exact IsClopen.diff (isClopen_closedBall _ hr) (isClopen_ball _ _)

/-- A sphere centred at the origin in a valued ring is open. -/
theorem isOpen_sphere {r : Œì‚ÇÄ} (hr : r ‚â† 0) : IsOpen (X := R) {x | v x = r} :=
  isClopen_sphere _ hr |>.isOpen

/-- A sphere centred at the origin in a valued ring is closed. -/
theorem isClosed_sphere (r : Œì‚ÇÄ) : IsClosed (X := R) {x | v x = r} := by
  rcases eq_or_ne r 0 with rfl|hr
  ¬∑ simpa using isClosed_closedBall R 0
  exact isClopen_sphere _ hr |>.isClosed

/-- The closed unit ball in a valued ring is open. -/
theorem isOpen_integer : IsOpen (_i.v.integer : Set R) :=
  isOpen_closedBall _ one_ne_zero

@[deprecated (since := "2025-04-25")]
alias integer_isOpen := isOpen_integer

/-- The closed unit ball of a valued ring is closed. -/
theorem isClosed_integer : IsClosed (_i.v.integer : Set R) :=
  isClosed_closedBall _ _

/-- The closed unit ball of a valued ring is clopen. -/
theorem isClopen_integer : IsClopen (_i.v.integer : Set R) :=
  ‚ü®isClosed_integer _, isOpen_integer _‚ü©

/-- The valuation subring of a valued field is open. -/
theorem isOpen_valuationSubring (K : Type u) [Field K] [hv : Valued K Œì‚ÇÄ] :
    IsOpen (hv.v.valuationSubring : Set K) :=
  isOpen_integer K

@[deprecated (since := "2025-04-25")]
alias valuationSubring_isOpen := isOpen_valuationSubring

/-- The valuation subring of a valued field is closed. -/
theorem isClosed_valuationSubring (K : Type u) [Field K] [hv : Valued K Œì‚ÇÄ] :
    IsClosed (hv.v.valuationSubring : Set K) :=
  isClosed_integer K

/-- The valuation subring of a valued field is clopen. -/
theorem isClopen_valuationSubring (K : Type u) [Field K] [hv : Valued K Œì‚ÇÄ] :
    IsClopen (hv.v.valuationSubring : Set K) :=
  isClopen_integer K

section Ideal

local notation "ùìû" => _i.v.integer

/-- The submodule of over the valuation subring whose valuation is less than or equal to a
certain value. -/
def leSubmodule (Œ≥ : Œì‚ÇÄ) : Submodule ùìû R where
  __ := leAddSubgroup v Œ≥
  smul_mem' r x h := by
    simpa [Subring.smul_def] using mul_le_of_le_one_of_le r.prop h

/-- The submodule of over the valuation subring whose valuation is less than a certain unit. -/
def ltSubmodule (Œ≥ : Œì‚ÇÄÀ£) : Submodule ùìû R where
  __ := ltAddSubgroup v Œ≥
  smul_mem' r x h := by
    simpa [Subring.smul_def] using mul_lt_of_le_one_of_lt r.prop h

lemma leSubmodule_mono : Monotone (leSubmodule R) :=
  leAddSubgroup_mono v

lemma ltSubmodule_mono : Monotone (ltSubmodule R) :=
  ltAddSubgroup_mono v

lemma ltSubmodule_le_leSubmodule (Œ≥ : Œì‚ÇÄÀ£) :
    ltSubmodule R Œ≥ ‚â§ leSubmodule R (Œ≥ : Œì‚ÇÄ) :=
  ltAddSubgroup_le_leAddSubgroup v Œ≥

lemma isOpen_ltSubmodule (Œ≥ : Œì‚ÇÄÀ£) :
    IsOpen (ltSubmodule R Œ≥ : Set R) :=
  isOpen_ball _ _

lemma isClosed_ltSubmodule (Œ≥ : Œì‚ÇÄÀ£) :
    IsClosed (ltSubmodule R Œ≥ : Set R) :=
  isClosed_ball _ _

lemma isClopen_ltSubmodule (Œ≥ : Œì‚ÇÄÀ£) :
    IsClopen (ltSubmodule R Œ≥ : Set R) :=
  isClopen_ball _ _

lemma isOpen_leSubmodule {Œ≥ : Œì‚ÇÄ} (hŒ≥ : Œ≥ ‚â† 0) :
    IsOpen (leSubmodule R Œ≥ : Set R) :=
  isOpen_closedBall _ hŒ≥

lemma isClosed_leSubmodule (Œ≥ : Œì‚ÇÄ) :
    IsClosed (leSubmodule R Œ≥ : Set R) :=
  isClosed_closedBall _ _

lemma isClopen_leSubmodule {Œ≥ : Œì‚ÇÄ} (hŒ≥ : Œ≥ ‚â† 0) :
    IsClopen (leSubmodule R Œ≥ : Set R) :=
  isClopen_closedBall _ hŒ≥

variable {R} in
@[simp]
lemma mem_leSubmodule_iff {Œ≥ : Œì‚ÇÄ} {x : R} :
    x ‚àà leSubmodule R Œ≥ ‚Üî v x ‚â§ Œ≥ :=
  Iff.rfl

variable {R} in
@[simp]
lemma mem_ltSubmodule_iff {Œ≥ : Œì‚ÇÄÀ£} {x : R} :
    x ‚àà ltSubmodule R Œ≥ ‚Üî v x < Œ≥ :=
  Iff.rfl

@[simp]
lemma leSubmodule_zero (K : Type u) [Field K] [hv : Valued K Œì‚ÇÄ] :
    leSubmodule K (0 : Œì‚ÇÄ) = ‚ä• := by
  ext; simp

--- the ideals do not use the submodules due to `Ideal.comap` requiring commutativity

/-- The ideal of elements of the valuation subring whose valuation is less than or equal to a
certain value. -/
def leIdeal (Œ≥ : Œì‚ÇÄ) : Ideal ùìû where
  __ := AddSubgroup.addSubgroupOf (leAddSubgroup v Œ≥) _i.v.integer.toAddSubgroup
  smul_mem' r x h := by
    change v ((r : R) * x) ‚â§ Œ≥ -- not sure why simp can't get us to here
    simpa [Subring.smul_def] using mul_le_of_le_one_of_le r.prop h

/-- The ideal of elements of the valuation subring whose valuation is less than a certain unit. -/
def ltIdeal (Œ≥ : Œì‚ÇÄÀ£) : Ideal ùìû where
  __ := AddSubgroup.addSubgroupOf (ltAddSubgroup v Œ≥) _i.v.integer.toAddSubgroup
  smul_mem' r x h := by
    change v ((r : R) * x) < Œ≥ -- not sure why simp can't get us to here
    simpa [Subring.smul_def] using mul_lt_of_le_one_of_lt r.prop h

-- Can't use `leAddSubgroup` because `addSubgroupOf` is a dependent function
lemma leIdeal_mono : Monotone (leIdeal R) :=
  fun _ _ h _ ‚Ü¶ h.trans'

lemma ltIdeal_mono : Monotone (ltIdeal R) :=
  fun _ _ h _ ‚Ü¶ (Units.val_le_val.mpr h).trans_lt'

lemma ltIdeal_le_leIdeal (Œ≥ : Œì‚ÇÄÀ£) :
    ltIdeal R Œ≥ ‚â§ leIdeal R (Œ≥ : Œì‚ÇÄ) :=
  fun _ h ‚Ü¶ h.le

variable {R} in
@[simp]
lemma mem_leIdeal_iff {Œ≥ : Œì‚ÇÄ} {x : ùìû} :
    x ‚àà leIdeal R Œ≥ ‚Üî v (x : R) ‚â§ Œ≥ :=
  Iff.rfl

variable {R} in
@[simp]
lemma mem_ltIdeal_iff {Œ≥ : Œì‚ÇÄÀ£} {x : ùìû} :
    x ‚àà ltIdeal R Œ≥ ‚Üî v (x : R) < Œ≥ :=
  Iff.rfl

@[simp]
lemma leIdeal_zero (K : Type u) [Field K] [hv : Valued K Œì‚ÇÄ] :
    leIdeal K (0 : Œì‚ÇÄ) = ‚ä• := by
  ext; simp

lemma isOpen_ltIdeal (Œ≥ : Œì‚ÇÄÀ£) :
    IsOpen (ltIdeal R Œ≥ : Set ùìû) :=
  isOpen_ball _ _ |>.preimage continuous_subtype_val

lemma isClosed_ltIdeal (Œ≥ : Œì‚ÇÄÀ£) :
    IsClosed (ltIdeal R Œ≥ : Set ùìû) :=
  isClosed_ball _ _ |>.preimage continuous_subtype_val

lemma isClopen_ltIdeal (Œ≥ : Œì‚ÇÄÀ£) :
    IsClopen (ltIdeal R Œ≥ : Set ùìû) :=
  isClopen_ball _ _ |>.preimage continuous_subtype_val

lemma isOpen_leIdeal {Œ≥ : Œì‚ÇÄ} (hŒ≥ : Œ≥ ‚â† 0) :
    IsOpen (leIdeal R Œ≥ : Set ùìû) :=
  isOpen_closedBall _ hŒ≥ |>.preimage continuous_subtype_val

lemma isClosed_leIdeal (Œ≥ : Œì‚ÇÄ) :
    IsClosed (leIdeal R Œ≥ : Set ùìû) :=
  isClosed_closedBall _ _ |>.preimage continuous_subtype_val

lemma isClopen_leIdeal {Œ≥ : Œì‚ÇÄ} (hŒ≥ : Œ≥ ‚â† 0) :
    IsClopen (leIdeal R Œ≥ : Set ùìû) :=
  isClopen_closedBall _ hŒ≥ |>.preimage continuous_subtype_val

end Ideal

end Valued
