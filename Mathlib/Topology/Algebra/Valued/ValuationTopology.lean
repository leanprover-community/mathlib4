/-
Copyright (c) 2021 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
import Mathlib.Algebra.Order.Group.Units
import Mathlib.Topology.Algebra.Nonarchimedean.Bases
import Mathlib.Topology.Algebra.UniformFilterBasis
import Mathlib.RingTheory.Valuation.ValuationSubring

/-!
# The topology on a valued ring

In this file, we define the non archimedean topology induced by a valuation on a ring.
The main definition is a `Valued` type class which equips a ring with a valuation taking
values in a group with zero. Other instances are then deduced from this.

*NOTE* (2025-07-02):
The `Valued` class defined in this file will eventually get replaced with `ValuativeRel`
from `Mathlib.RingTheory.Valuation.ValuativeRel`. New developments on valued rings/fields
should take this into considation.

-/

open scoped Topology uniformity
open Set Valuation

noncomputable section

universe v u

variable {R : Type u} [Ring R] {Œì‚ÇÄ : Type v} [LinearOrderedCommGroupWithZero Œì‚ÇÄ]

namespace Valuation

variable (v : Valuation R Œì‚ÇÄ)

/-- The basis of open subgroups for the topology on a ring determined by a valuation. -/
theorem subgroups_basis : RingSubgroupsBasis fun Œ≥ : Œì‚ÇÄÀ£ => (v.ltAddSubgroup Œ≥ : AddSubgroup R) :=
  { inter := by
      rintro Œ≥‚ÇÄ Œ≥‚ÇÅ
      use min Œ≥‚ÇÄ Œ≥‚ÇÅ
      simp only [ltAddSubgroup, Units.min_val, lt_inf_iff, le_inf_iff, AddSubgroup.mk_le_mk,
        AddSubmonoid.mk_le_mk, AddSubsemigroup.mk_le_mk, setOf_subset_setOf]
      tauto
    mul := by
      rintro Œ≥
      obtain ‚ü®Œ≥‚ÇÄ, h‚ü© := exists_square_le Œ≥
      use Œ≥‚ÇÄ
      rintro - ‚ü®r, r_in, s, s_in, rfl‚ü©
      simp only [ltAddSubgroup, AddSubgroup.coe_set_mk, AddSubmonoid.coe_set_mk,
        AddSubsemigroup.coe_set_mk, mem_setOf_eq] at r_in s_in
      calc
        (v (r * s) : Œì‚ÇÄ) = v r * v s := Valuation.map_mul _ _ _
        _ < Œ≥‚ÇÄ * Œ≥‚ÇÄ := by gcongr <;> exact zero_le'
        _ ‚â§ Œ≥ := mod_cast h
    leftMul := by
      rintro x Œ≥
      rcases GroupWithZero.eq_zero_or_unit (v x) with (Hx | ‚ü®Œ≥x, Hx‚ü©)
      ¬∑ use (1 : Œì‚ÇÄÀ£)
        rintro y _
        change v (x * y) < _
        rw [Valuation.map_mul, Hx, zero_mul]
        exact Units.zero_lt Œ≥
      ¬∑ use Œ≥x‚Åª¬π * Œ≥
        rintro y (vy_lt : v y < ‚Üë(Œ≥x‚Åª¬π * Œ≥))
        change (v (x * y) : Œì‚ÇÄ) < Œ≥
        rw [Valuation.map_mul, Hx, mul_comm]
        rw [Units.val_mul, mul_comm] at vy_lt
        simpa using mul_inv_lt_of_lt_mul‚ÇÄ vy_lt
    rightMul := by
      rintro x Œ≥
      rcases GroupWithZero.eq_zero_or_unit (v x) with (Hx | ‚ü®Œ≥x, Hx‚ü©)
      ¬∑ use 1
        rintro y _
        change v (y * x) < _
        rw [Valuation.map_mul, Hx, mul_zero]
        exact Units.zero_lt Œ≥
      ¬∑ use Œ≥x‚Åª¬π * Œ≥
        rintro y (vy_lt : v y < ‚Üë(Œ≥x‚Åª¬π * Œ≥))
        change (v (y * x) : Œì‚ÇÄ) < Œ≥
        rw [Valuation.map_mul, Hx]
        rw [Units.val_mul, mul_comm] at vy_lt
        simpa using mul_inv_lt_of_lt_mul‚ÇÄ vy_lt }

end Valuation

/-- A valued ring is a ring that comes equipped with a distinguished valuation. The class `Valued`
is designed for the situation that there is a canonical valuation on the ring.

TODO: show that there always exists an equivalent valuation taking values in a type belonging to
the same universe as the ring.

See Note [forgetful inheritance] for why we extend `UniformSpace`, `IsUniformAddGroup`. -/
class Valued (R : Type u) [Ring R] (Œì‚ÇÄ : outParam (Type v))
  [LinearOrderedCommGroupWithZero Œì‚ÇÄ] extends UniformSpace R, IsUniformAddGroup R where
  v : Valuation R Œì‚ÇÄ
  is_topological_valuation : ‚àÄ s, s ‚àà ùìù (0 : R) ‚Üî ‚àÉ Œ≥ : Œì‚ÇÄÀ£, { x : R | v x < Œ≥ } ‚äÜ s

namespace Valued

/-- Alternative `Valued` constructor for use when there is no preferred `UniformSpace` structure. -/
def mk' (v : Valuation R Œì‚ÇÄ) : Valued R Œì‚ÇÄ :=
  { v
    toUniformSpace := @IsTopologicalAddGroup.toUniformSpace R _ v.subgroups_basis.topology _
    toIsUniformAddGroup := @isUniformAddGroup_of_addCommGroup _ _ v.subgroups_basis.topology _
    is_topological_valuation := by
      letI := @IsTopologicalAddGroup.toUniformSpace R _ v.subgroups_basis.topology _
      intro s
      rw [Filter.hasBasis_iff.mp v.subgroups_basis.hasBasis_nhds_zero s]
      exact exists_congr fun Œ≥ => by rw [true_and]; rfl }

variable (R Œì‚ÇÄ)
variable [_i : Valued R Œì‚ÇÄ]

theorem hasBasis_nhds_zero :
    (ùìù (0 : R)).HasBasis (fun _ => True) fun Œ≥ : Œì‚ÇÄÀ£ => { x | v x < (Œ≥ : Œì‚ÇÄ) } := by
  simp [Filter.hasBasis_iff, is_topological_valuation]

open Uniformity in
theorem hasBasis_uniformity : (ùì§ R).HasBasis (fun _ => True)
    fun Œ≥ : Œì‚ÇÄÀ£ => { p : R √ó R | v (p.2 - p.1) < (Œ≥ : Œì‚ÇÄ) } := by
  rw [uniformity_eq_comap_nhds_zero]
  exact (hasBasis_nhds_zero R Œì‚ÇÄ).comap _

theorem toUniformSpace_eq :
    toUniformSpace = @IsTopologicalAddGroup.toUniformSpace R _ v.subgroups_basis.topology _ :=
  UniformSpace.ext
    ((hasBasis_uniformity R Œì‚ÇÄ).eq_of_same_basis <| v.subgroups_basis.hasBasis_nhds_zero.comap _)

variable {R Œì‚ÇÄ}

theorem mem_nhds {s : Set R} {x : R} : s ‚àà ùìù x ‚Üî ‚àÉ Œ≥ : Œì‚ÇÄÀ£, { y | (v (y - x) : Œì‚ÇÄ) < Œ≥ } ‚äÜ s := by
  simp only [‚Üê nhds_translation_add_neg x, ‚Üê sub_eq_add_neg, preimage_setOf_eq, true_and,
    ((hasBasis_nhds_zero R Œì‚ÇÄ).comap fun y => y - x).mem_iff]

theorem mem_nhds_zero {s : Set R} : s ‚àà ùìù (0 : R) ‚Üî ‚àÉ Œ≥ : Œì‚ÇÄÀ£, { x | v x < (Œ≥ : Œì‚ÇÄ) } ‚äÜ s := by
  simp only [mem_nhds, sub_zero]

theorem loc_const {x : R} (h : (v x : Œì‚ÇÄ) ‚â† 0) : { y : R | v y = v x } ‚àà ùìù x := by
  rw [mem_nhds]
  use Units.mk0 _ h
  rw [Units.val_mk0]
  intro y y_in
  exact Valuation.map_eq_of_sub_lt _ y_in

instance (priority := 100) : IsTopologicalRing R :=
  (toUniformSpace_eq R Œì‚ÇÄ).symm ‚ñ∏ v.subgroups_basis.toRingFilterBasis.isTopologicalRing

theorem cauchy_iff {F : Filter R} : Cauchy F ‚Üî
    F.NeBot ‚àß ‚àÄ Œ≥ : Œì‚ÇÄÀ£, ‚àÉ M ‚àà F, ‚àÄ·µâ (x ‚àà M) (y ‚àà M), (v (y - x) : Œì‚ÇÄ) < Œ≥ := by
  rw [toUniformSpace_eq, AddGroupFilterBasis.cauchy_iff]
  apply and_congr Iff.rfl
  simp_rw [Valued.v.subgroups_basis.mem_addGroupFilterBasis_iff]
  constructor
  ¬∑ intro h Œ≥
    exact h _ (Valued.v.subgroups_basis.mem_addGroupFilterBasis _)
  ¬∑ rintro h - ‚ü®Œ≥, rfl‚ü©
    exact h Œ≥

variable (R)

/-- An open ball centred at the origin in a valued ring is open. -/
theorem isOpen_ball (r : Œì‚ÇÄ) : IsOpen (X := R) {x | v x < r} := by
  rw [isOpen_iff_mem_nhds]
  rcases eq_or_ne r 0 with rfl | hr
  ¬∑ simp
  intro x hx
  rw [mem_nhds]
  simp only [setOf_subset_setOf]
  exact ‚ü®Units.mk0 _ hr,
    fun y hy => (sub_add_cancel y x).symm ‚ñ∏ (v.map_add _ x).trans_lt (max_lt hy hx)‚ü©

/-- An open ball centred at the origin in a valued ring is closed. -/
theorem isClosed_ball (r : Œì‚ÇÄ) : IsClosed (X := R) {x | v x < r} := by
  rcases eq_or_ne r 0 with rfl | hr
  ¬∑ simp
  exact AddSubgroup.isClosed_of_isOpen
    (Valuation.ltAddSubgroup v (Units.mk0 r hr))
    (isOpen_ball _ _)

/-- An open ball centred at the origin in a valued ring is clopen. -/
theorem isClopen_ball (r : Œì‚ÇÄ) : IsClopen (X := R) {x | v x < r} :=
  ‚ü®isClosed_ball _ _, isOpen_ball _ _‚ü©

/-- A closed ball centred at the origin in a valued ring is open. -/
theorem isOpen_closedball {r : Œì‚ÇÄ} (hr : r ‚â† 0) : IsOpen (X := R) {x | v x ‚â§ r} := by
  rw [isOpen_iff_mem_nhds]
  intro x hx
  rw [mem_nhds]
  simp only [setOf_subset_setOf]
  exact ‚ü®Units.mk0 _ hr,
    fun y hy => (sub_add_cancel y x).symm ‚ñ∏ le_trans (v.map_add _ _) (max_le (le_of_lt hy) hx)‚ü©

/-- A closed ball centred at the origin in a valued ring is closed. -/
theorem isClosed_closedBall (r : Œì‚ÇÄ) : IsClosed (X := R) {x | v x ‚â§ r} := by
  rw [‚Üê isOpen_compl_iff, isOpen_iff_mem_nhds]
  intro x hx
  rw [mem_nhds]
  have hx' : v x ‚â† 0 := ne_of_gt <| lt_of_le_of_lt zero_le' <| lt_of_not_ge hx
  exact ‚ü®Units.mk0 _ hx', fun y hy hy' => ne_of_lt hy <| map_sub_swap v x y ‚ñ∏
      (Valuation.map_sub_eq_of_lt_left _ <| lt_of_le_of_lt hy' (lt_of_not_ge hx))‚ü©

/-- A closed ball centred at the origin in a valued ring is clopen. -/
theorem isClopen_closedBall {r : Œì‚ÇÄ} (hr : r ‚â† 0) : IsClopen (X := R) {x | v x ‚â§ r} :=
  ‚ü®isClosed_closedBall _ _, isOpen_closedball _ hr‚ü©

/-- A sphere centred at the origin in a valued ring is clopen. -/
theorem isClopen_sphere {r : Œì‚ÇÄ} (hr : r ‚â† 0) : IsClopen (X := R) {x | v x = r} := by
  have h : {x : R | v x = r} = {x | v x ‚â§ r} \ {x | v x < r} := by
    ext x
    simp [‚Üê le_antisymm_iff]
  rw [h]
  exact IsClopen.diff (isClopen_closedBall _ hr) (isClopen_ball _ _)

/-- A sphere centred at the origin in a valued ring is open. -/
theorem isOpen_sphere {r : Œì‚ÇÄ} (hr : r ‚â† 0) : IsOpen (X := R) {x | v x = r} :=
  isClopen_sphere _ hr |>.isOpen

/-- A sphere centred at the origin in a valued ring is closed. -/
theorem isClosed_sphere (r : Œì‚ÇÄ) : IsClosed (X := R) {x | v x = r} := by
  rcases eq_or_ne r 0 with rfl | hr
  ¬∑ simpa using isClosed_closedBall R 0
  exact isClopen_sphere _ hr |>.isClosed

/-- The closed unit ball in a valued ring is open. -/
theorem isOpen_integer : IsOpen (_i.v.integer : Set R) :=
  isOpen_closedball _ one_ne_zero

@[deprecated (since := "2025-04-25")]
alias integer_isOpen := isOpen_integer

/-- The closed unit ball of a valued ring is closed. -/
theorem isClosed_integer : IsClosed (_i.v.integer : Set R) :=
  isClosed_closedBall _ _

/-- The closed unit ball of a valued ring is clopen. -/
theorem isClopen_integer : IsClopen (_i.v.integer : Set R) :=
  ‚ü®isClosed_integer _, isOpen_integer _‚ü©

/-- The valuation subring of a valued field is open. -/
theorem isOpen_valuationSubring (K : Type u) [Field K] [hv : Valued K Œì‚ÇÄ] :
    IsOpen (hv.v.valuationSubring : Set K) :=
  isOpen_integer K

@[deprecated (since := "2025-04-25")]
alias valuationSubring_isOpen := isOpen_valuationSubring

/-- The valuation subring of a valued field is closed. -/
theorem isClosed_valuationSubring (K : Type u) [Field K] [hv : Valued K Œì‚ÇÄ] :
    IsClosed (hv.v.valuationSubring : Set K) :=
  isClosed_integer K

/-- The valuation subring of a valued field is clopen. -/
theorem isClopen_valuationSubring (K : Type u) [Field K] [hv : Valued K Œì‚ÇÄ] :
    IsClopen (hv.v.valuationSubring : Set K) :=
  isClopen_integer K

end Valued
