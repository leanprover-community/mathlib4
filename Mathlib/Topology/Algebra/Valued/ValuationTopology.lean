/-
Copyright (c) 2021 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
import Mathlib.Topology.Algebra.Nonarchimedean.Bases
import Mathlib.Topology.Algebra.UniformFilterBasis
import Mathlib.RingTheory.Valuation.ValuationSubring

/-!
# The topology on a valued ring

In this file, we define the non archimedean topology induced by a valuation on a ring.
The main definition is a `Valued` type class which equips a ring with a valuation taking
values in a group with zero. Other instances are then deduced from this.
-/

open scoped Topology uniformity
open Set Valuation

noncomputable section

universe v u

variable {R : Type u} [Ring R] {Î“â‚€ : Type v} [LinearOrderedCommGroupWithZero Î“â‚€]

namespace Valuation

variable (v : Valuation R Î“â‚€)

instance {M : Type*} [Monoid M] {r : M â†’ M â†’ Prop}
    [CovariantClass M M (Â· * Â·) r] (N : Submonoid M):
      CovariantClass N N (Â· * Â·) (fun x y â†¦ r x y) := âŸ¨ by
  rintro âŸ¨m, _âŸ© âŸ¨nâ‚, _âŸ© âŸ¨nâ‚‚, _âŸ©
  simpa only [Submonoid.mk_mul_mk] using CovariantClass.elim m âŸ©

instance {M : Type*} [Monoid M] [LT M]
    [CovariantClass M M (Â· * Â·) (Â· < Â·)] (N : Submonoid M):
      CovariantClass N N (Â· * Â·) (fun x y â†¦ x < y) := âŸ¨ by
  rintro âŸ¨m, _âŸ© âŸ¨nâ‚, _âŸ© âŸ¨nâ‚‚, _âŸ©
  simp only [Subtype.mk_lt_mk, Submonoid.mk_mul_mk]
  exact fun a â†¦ mul_lt_mul_left' a m
  âŸ©

/-- The basis of open subgroups for the topology on a ring determined by a valuation. -/
theorem subgroups_basis :
    RingSubgroupsBasis fun Î³ : v.rangeGroup => (v.ltAddSubgroup Î³ : AddSubgroup R) :=
  { inter := by
      intro Î³â‚€ Î³â‚
      use min Î³â‚€ Î³â‚
      simp only [LinearOrderedCommGroup.min_def, Subtype.mk_le_mk, le_inf_iff]
      refine âŸ¨fun a ha â†¦ ?_ , fun a ha â†¦ ?_âŸ© <;>
      split_ifs at ha with ha1 <;>
      try exact ha
      try apply lt_of_lt_of_le ha
      Â· exact le_of_lt <| lt_of_not_ge ha1
      Â· exact lt_of_lt_of_le ha ha1
    mul := by
      rintro Î³
      obtain âŸ¨Î³â‚€, hâŸ© := exists_square_le Î³
      use Î³â‚€
      rintro - âŸ¨r, r_in, s, s_in, rflâŸ©
      simp only [SetLike.mem_coe, mem_ltAddSubgroup_iff, _root_.map_mul]
      apply lt_of_lt_of_le (mul_lt_mulâ‚€ r_in s_in) h
    leftMul := by
      rintro x Î³
      rcases GroupWithZero.eq_zero_or_unit (v x) with (Hx | âŸ¨Î³x, HxâŸ©)
      Â· use 1
        rintro y _
        change v (x * y) < _
        simp only [_root_.map_mul, Hx, zero_mul, Units.zero_lt]
      Â· use âŸ¨Î³x, mem_rangeGroup v HxâŸ©â»Â¹ * Î³
        rintro y (vy_lt : v y < â†‘(Î³xâ»Â¹ * Î³))
        simp only [mem_preimage, SetLike.mem_coe, mem_ltAddSubgroup_iff, _root_.map_mul]
        rw [Hx, mul_comm]
        rw [Units.val_mul, mul_comm] at vy_lt
        simpa using mul_inv_lt_of_lt_mulâ‚€ vy_lt
    rightMul := by
      rintro x Î³
      rcases GroupWithZero.eq_zero_or_unit (v x) with (Hx | âŸ¨Î³x, HxâŸ©)
      Â· use 1
        rintro y _
        change v (y * x) < _
        rw [Valuation.map_mul, Hx, mul_zero]
        exact @Units.zero_lt Î“â‚€ _ Î³
      Â· use âŸ¨Î³x, mem_rangeGroup v HxâŸ©â»Â¹ * Î³
        rintro y (vy_lt : v y < â†‘(Î³xâ»Â¹ * Î³))
        simp only [mem_preimage, SetLike.mem_coe, mem_ltAddSubgroup_iff, _root_.map_mul, Hx]
        rw [Units.val_mul, mul_comm] at vy_lt
        simpa using mul_inv_lt_of_lt_mulâ‚€ vy_lt }

end Valuation

/-- A valued ring is a ring that comes equipped with a distinguished group-valued valuation.
The class `Valued` is designed for the situation that there is a canonical valuation on the ring.

TODO: show that there always exists an equivalent valuation taking values in a type belonging to
the same universe as the ring.

See Note [forgetful inheritance] for why we extend `UniformSpace`, `UniformAddGroup`. -/
class Valued (R : Type u) [Ring R] (Î“â‚€ : outParam (Type v))
  [LinearOrderedCommGroupWithZero Î“â‚€] extends UniformSpace R, UniformAddGroup R where
  v : Valuation R Î“â‚€
  is_topological_valuation : âˆ€ s, s âˆˆ ğ“ (0 : R) â†”
    âˆƒ Î³ âˆˆ v.rangeGroup, { x : R | v x < Î³ } âŠ† s

namespace Valued

/-- Alternative `Valued` constructor for use when there is no preferred `UniformSpace` structure. -/
def mk' (v : Valuation R Î“â‚€) : Valued R Î“â‚€ :=
  { v
    toUniformSpace := @IsTopologicalAddGroup.toUniformSpace R _ v.subgroups_basis.topology _
    toUniformAddGroup := @comm_topologicalAddGroup_is_uniform _ _ v.subgroups_basis.topology _
    is_topological_valuation := by
      letI := @IsTopologicalAddGroup.toUniformSpace R _ v.subgroups_basis.topology _
      intro s
      rw [Filter.hasBasis_iff.mp v.subgroups_basis.hasBasis_nhds_zero s]
      simp only [true_and, Subtype.exists, exists_prop]
      exact rfl.to_iff
      }

variable (R Î“â‚€)
variable [_i : Valued R Î“â‚€]

theorem hasBasis_nhds_zero :
    (ğ“ (0 : R)).HasBasis (fun _ => True) fun Î³ : _i.v.rangeGroup => { x | v x < (Î³ : Î“â‚€Ë£) } := by
  simp [Filter.hasBasis_iff, is_topological_valuation]

-- Porting note: Replaced `ğ“¤ R` with `uniformity R`
theorem hasBasis_uniformity : (uniformity R).HasBasis (fun _ => True)
    fun Î³ : _i.v.rangeGroup => { p : R Ã— R | v (p.2 - p.1) < (Î³ : Î“â‚€Ë£) } := by
  rw [uniformity_eq_comap_nhds_zero]
  exact (hasBasis_nhds_zero R Î“â‚€).comap _

theorem toUniformSpace_eq :
    toUniformSpace = @IsTopologicalAddGroup.toUniformSpace R _ v.subgroups_basis.topology _ :=
  UniformSpace.ext
    ((hasBasis_uniformity R Î“â‚€).eq_of_same_basis <| v.subgroups_basis.hasBasis_nhds_zero.comap _)

variable {R Î“â‚€}

theorem mem_nhds {s : Set R} {x : R} : s âˆˆ ğ“ x â†” âˆƒ Î³ : _i.v.rangeGroup,
    { y | v (y - x) < (Î³ : Î“â‚€Ë£) } âŠ† s := by
  simp only [â† nhds_translation_add_neg x, â† sub_eq_add_neg, preimage_setOf_eq, true_and,
    ((hasBasis_nhds_zero R Î“â‚€).comap fun y => y - x).mem_iff]

theorem mem_nhds_zero {s : Set R} : s âˆˆ ğ“ (0 : R) â†”
    âˆƒ Î³ : _i.v.rangeGroup, { x | _i.v x < (Î³ : Î“â‚€Ë£) } âŠ† s := by
  simp only [mem_nhds, sub_zero]

theorem loc_const {x : R} (h : (v x : Î“â‚€) â‰  0) : { y : R | v y = v x } âˆˆ ğ“ x := by
  rw [mem_nhds]
  use âŸ¨_, (mem_rangeGroup v rfl : Units.mk0 _ h âˆˆ _i.v.rangeGroup)âŸ©
  intro y
  exact Valuation.map_eq_of_sub_lt _

instance (priority := 100) : IsTopologicalRing R :=
  (toUniformSpace_eq R Î“â‚€).symm â–¸ v.subgroups_basis.toRingFilterBasis.isTopologicalRing

theorem cauchy_iff {F : Filter R} : Cauchy F â†”
    F.NeBot âˆ§ âˆ€ Î³ : _i.v.rangeGroup, âˆƒ M âˆˆ F, âˆ€áµ‰ (x âˆˆ M) (y âˆˆ M), v (y - x) < (Î³ : Î“â‚€Ë£) := by
  rw [toUniformSpace_eq, AddGroupFilterBasis.cauchy_iff]
  apply and_congr Iff.rfl
  simp_rw [Valued.v.subgroups_basis.mem_addGroupFilterBasis_iff]
  constructor
  Â· intro h Î³
    exact h _ (Valued.v.subgroups_basis.mem_addGroupFilterBasis _)
  Â· rintro h - âŸ¨Î³, rflâŸ©
    exact h Î³

variable (R)

/-- The unit ball of a valued ring is open. -/
theorem integer_isOpen : IsOpen (_i.v.integer : Set R) := by
  rw [isOpen_iff_mem_nhds]
  intro x hx
  rw [mem_nhds]
  exact âŸ¨1,
    fun y hy => (sub_add_cancel y x).symm â–¸ le_trans (map_add _ _ _) (max_le (le_of_lt hy) hx)âŸ©

/-- The valuation subring of a valued field is open. -/
theorem valuationSubring_isOpen (K : Type u) [Field K] [hv : Valued K Î“â‚€] :
    IsOpen (hv.v.valuationSubring : Set K) :=
  integer_isOpen K

end Valued
