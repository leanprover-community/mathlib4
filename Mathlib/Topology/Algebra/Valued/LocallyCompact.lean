/-
Copyright (c) 2024 Yakov Pechersky. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yakov Pechersky
-/
import Mathlib.RingTheory.DiscreteValuationRing.Basic
import Mathlib.RingTheory.Ideal.IsPrincipalPowQuotient
import Mathlib.Topology.Algebra.Valued.NormedValued
import Mathlib.Topology.Algebra.Valued.ValuedField

/-!
# Necessary and sufficient conditions for a locally compact nonarchimedean normed field

## Main Definitions
* `totallyBounded_iff_finite_residueField`: when the valuation ring is a DVR,
  it is totally bounded iff the residue field is finite.

## Tags

norm, nonarchimedean, rank one, compact, locally compact
-/

variable {K : Type*} [NontriviallyNormedField K] [IsUltrametricDist K]

open NNReal

open scoped NormedField

@[simp]
lemma NormedField.v_eq_valuation (x : K) : Valued.v x = NormedField.valuation x := rfl

namespace Valued.integer

-- should we do this all in the Valuation namespace instead?

/-- An element is in the valuation ring if the norm is bounded by 1. This is a variant of
`Valuation.mem_integer_iff`, phrased using norms instead of the valuation. -/
lemma mem_iff {x : K} : x âˆˆ ğ’ª[K] â†” â€–xâ€– â‰¤ 1 := by
  simp [Valuation.mem_integer_iff, â† NNReal.coe_le_coe]

lemma norm_le_one (x : ğ’ª[K]) : â€–xâ€– â‰¤ 1 := mem_iff.mp x.prop

@[simp]
lemma norm_coe_unit (u : ğ’ª[K]Ë£) : â€–((u : ğ’ª[K]) : K)â€– = 1 := by
  simpa [â† NNReal.coe_inj] using
    (Valuation.integer.integers (NormedField.valuation (K := K))).valuation_unit u

lemma norm_unit (u : ğ’ª[K]Ë£) : â€–(u : ğ’ª[K])â€– = 1 := by
  simp

lemma isUnit_iff_norm_eq_one {u : ğ’ª[K]} : IsUnit u â†” â€–uâ€– = 1 := by
  simpa [â† NNReal.coe_inj] using
    (Valuation.integer.integers (NormedField.valuation (K := K))).isUnit_iff_valuation_eq_one

lemma norm_irreducible_lt_one {Ï– : ğ’ª[K]} (h : Irreducible Ï–) : â€–Ï–â€– < 1 :=
  lt_of_le_of_ne (norm_le_one Ï–) (mt isUnit_iff_norm_eq_one.mpr h.not_unit)

lemma norm_irreducible_pos {Ï– : ğ’ª[K]} (h : Irreducible Ï–) : 0 < â€–Ï–â€– :=
  lt_of_le_of_ne (_root_.norm_nonneg Ï–) (by simp [eq_comm, h.ne_zero])

lemma coe_span_singleton_eq_closedBall (x : ğ’ª[K]) :
    (Ideal.span {x} : Set ğ’ª[K]) = Metric.closedBall 0 â€–xâ€– := by
  rcases eq_or_ne x 0 with rfl|hx
  Â· simp [Set.singleton_zero, Ideal.span_zero]
  ext y
  simp only [SetLike.mem_coe, Ideal.mem_span_singleton', AddSubgroupClass.coe_norm,
    Metric.mem_closedBall, dist_zero_right]
  constructor
  Â· rintro âŸ¨z, rflâŸ©
    simpa using mul_le_mul_of_nonneg_right (norm_le_one z) (_root_.norm_nonneg x)
  Â· intro h
    refine âŸ¨âŸ¨y / x, ?_âŸ©, ?_âŸ©
    Â· simpa [mem_iff] using div_le_one_of_leâ‚€ h (_root_.norm_nonneg _)
    Â· simpa only [Subtype.ext_iff] using div_mul_cancelâ‚€ (y : K) (by simpa using hx)

lemma _root_.Irreducible.maximalIdeal_eq_closedBall [IsDiscreteValuationRing ğ’ª[K]]
    {Ï– : ğ’ª[K]} (h : Irreducible Ï–) :
    (ğ“‚[K] : Set ğ’ª[K]) = Metric.closedBall 0 â€–Ï–â€– := by
  rw [â† coe_span_singleton_eq_closedBall, â† h.maximalIdeal_eq]

lemma _root_.Irreducible.maximalIdeal_pow_eq_closedBall_pow [IsDiscreteValuationRing ğ’ª[K]]
    {Ï– : ğ’ª[K]} (h : Irreducible Ï–) (n : â„•) :
    ((ğ“‚[K] ^ n : Ideal ğ’ª[K]) : Set ğ’ª[K]) = Metric.closedBall 0 (â€–Ï–â€– ^ n) := by
  have : â€–Ï–â€– ^ n = â€–Ï– ^ nâ€– := by simp
  rw [this, â† coe_span_singleton_eq_closedBall, â† Ideal.span_singleton_pow, â† h.maximalIdeal_eq]

section FiniteResidueField

variable {K : Type*} [NontriviallyNormedField K] [IsUltrametricDist K]

open Valued

lemma finite_quotient_maximalIdeal_pow_of_finite_residueField [IsDiscreteValuationRing ğ’ª[K]]
    (h : Finite ğ“€[K]) (n : â„•) :
    Finite (ğ’ª[K] â§¸ ğ“‚[K] ^ n) := by
  induction n with
  | zero =>
    simp only [pow_zero, Ideal.one_eq_top]
    exact Finite.of_fintype (â†¥ğ’ª[K] â§¸ âŠ¤)
  | succ n ih =>
    have : ğ“‚[K] ^ (n + 1) â‰¤ ğ“‚[K] ^ n := Ideal.pow_le_pow_right (by simp)
    replace ih := Finite.of_equiv _ (DoubleQuot.quotQuotEquivQuotOfLE this).symm.toEquiv
    suffices Finite (Ideal.map (Ideal.Quotient.mk (ğ“‚[K] ^ (n + 1))) (ğ“‚[K] ^ n)) from
      Finite.of_finite_quot_finite_ideal
        (I := Ideal.map (Ideal.Quotient.mk _) (ğ“‚[K] ^ n))
    exact @Finite.of_equiv _ _ h
      ((Ideal.quotEquivPowQuotPowSuccEquiv (IsPrincipalIdealRing.principal ğ“‚[K])
        (IsDiscreteValuationRing.not_a_field _) n).trans
        (Ideal.powQuotPowSuccEquivMapMkPowSuccPow _ n))

lemma totallyBounded_iff_finite_residueField [IsDiscreteValuationRing ğ’ª[K]] :
    TotallyBounded (Set.univ (Î± := ğ’ª[K])) â†” Finite ğ“€[K] := by
  constructor
  Â· intro H
    obtain âŸ¨p, hpâŸ© := IsDiscreteValuationRing.exists_irreducible ğ’ª[K]
    have := Metric.finite_approx_of_totallyBounded H â€–pâ€– (norm_pos_iff.mpr hp.ne_zero)
    simp only [Set.subset_univ, Set.univ_subset_iff, true_and] at this
    obtain âŸ¨t, ht, ht'âŸ© := this
    rw [â† Set.finite_univ_iff]
    refine (ht.image (IsLocalRing.residue _)).subset ?_
    rintro âŸ¨xâŸ©
    replace ht' := ht'.ge (Set.mem_univ x)
    simp only [Set.mem_iUnion, Metric.mem_ball, exists_prop] at ht'
    obtain âŸ¨y, hy, hy'âŸ© := ht'
    simp only [Submodule.Quotient.quot_mk_eq_mk, Ideal.Quotient.mk_eq_mk, Set.mem_univ,
      IsLocalRing.residue, Set.mem_image, true_implies]
    refine âŸ¨y, hy, ?_âŸ©
    convert (Ideal.Quotient.mk_eq_mk_iff_sub_mem (I := ğ“‚[K]) y x).mpr _
    -- TODO: make Valued.maximalIdeal abbreviations instead of def
    rw [Valued.maximalIdeal, hp.maximalIdeal_eq, â† SetLike.mem_coe,
      coe_span_singleton_eq_closedBall]
    rw [dist_comm] at hy'
    simpa [dist_eq_norm] using hy'.le
  Â· intro H
    rw [Metric.totallyBounded_iff]
    intro Îµ Îµpos
    obtain âŸ¨p, hpâŸ© := IsDiscreteValuationRing.exists_irreducible ğ’ª[K]
    have hp' := norm_irreducible_lt_one hp
    obtain âŸ¨n, hnâŸ© : âˆƒ n : â„•, â€–pâ€– ^ n < Îµ := exists_pow_lt_of_lt_one Îµpos hp'
    have hF := finite_quotient_maximalIdeal_pow_of_finite_residueField H n
    refine âŸ¨Quotient.out '' (Set.univ (Î± := ğ’ª[K] â§¸ (ğ“‚[K] ^ n))), Set.toFinite _, ?_âŸ©
    simp only [Ideal.univ_eq_iUnion_image_add (ğ“‚[K] ^ n), hp.maximalIdeal_pow_eq_closedBall_pow,
      AddSubgroupClass.coe_norm, Set.image_add_left, preimage_add_closedBall, sub_neg_eq_add,
      zero_add, Set.image_univ, Set.mem_range, Set.iUnion_exists, Set.iUnion_iUnion_eq',
      Set.iUnion_subset_iff, Metric.vadd_closedBall, vadd_eq_add, add_zero]
    intro
    exact (Metric.closedBall_subset_ball hn).trans (Set.subset_iUnion_of_subset _ le_rfl)

end FiniteResidueField

section CompactDVR

open Valued

variable (K) in
lemma exists_norm_coe_lt : âˆƒ x : ğ’ª[K], 0 < â€–(x : K)â€– âˆ§ â€–(x : K)â€– < 1 := by
  obtain âŸ¨x, hx, hx'âŸ© := NormedField.exists_norm_lt_one K
  refine âŸ¨âŸ¨x, hx'.leâŸ©, ?_âŸ©
  simpa [hx', Subtype.ext_iff] using hx

variable (K) in
lemma exists_norm_lt : âˆƒ x : ğ’ª[K], 0 < â€–xâ€– âˆ§ â€–xâ€– < 1 := by
  exact exists_norm_coe_lt K

variable (K) in
lemma exists_nnnorm_lt : âˆƒ x : ğ’ª[K], 0 < â€–xâ€–â‚Š âˆ§ â€–xâ€–â‚Š < 1 := by
  exact exists_norm_coe_lt K

lemma discreteValuationRing_of_compactSpace [h : CompactSpace ğ’ª[K]] :
    DiscreteValuationRing ğ’ª[K] := by
  have hl : LocalRing ğ’ª[K] := inferInstance
  obtain âŸ¨x, hx, hx'âŸ© := exists_nnnorm_lt K
  rw [â† nnnorm_one (K := K)] at hx'
  have hi : Valuation.Integers (R := K) Valued.v ğ’ª[K] := Valuation.integer.integers v
  have key : IsPrincipalIdealRing ğ’ª[K]:= by
    rw [hi.isPrincipalIdealRing_iff_not_denselyOrdered]
    intro H
    replace H : DenselyOrdered (Set.range ((â€–Â·â€–â‚Š) : ğ’ª[K] â†’ â„â‰¥0)) := by
      constructor
      rintro âŸ¨_, a, rflâŸ© âŸ¨_, b, rflâŸ© h
      replace h : (âŸ¨_, a, rflâŸ© : Set.range (v : K â†’ â„â‰¥0)) < âŸ¨_, b, rflâŸ© := h
      obtain âŸ¨âŸ¨_, c, rflâŸ©, hcâŸ© := exists_between h
      refine âŸ¨âŸ¨_, âŸ¨c, ?_âŸ©, rflâŸ©, hcâŸ©
      Â· rw [mem_integer_iff']
        simp only [v_eq_valuation, NormedField.valuation_apply, Subtype.mk_lt_mk, â† coe_lt_coe,
          coe_nnnorm] at hc
        simpa using hc.right.le.trans (mem_integer_iff'.mp b.prop)
    let U : ğ’ª[K] â†’ Set ğ’ª[K] := fun y â†¦ if â€–yâ€–â‚Š < â€–xâ€–â‚Š
      then Metric.closedBall 0 â€–xâ€–
      else Metric.sphere 0 â€–yâ€–
    obtain âŸ¨t, htâŸ© := CompactSpace.elim_nhds_subcover U <| by
      intro y
      simp only [AddSubgroupClass.coe_norm, U]
      split_ifs with hy
      Â· refine (IsUltrametricDist.isOpen_closedBall _ ?_).mem_nhds (by simpa using hy.le)
        simpa using hx
      Â· refine (IsUltrametricDist.isOpen_sphere _ ?_).mem_nhds (by simp)
        simpa using (hx.trans_le (le_of_not_lt hy)).ne'
    have htm : âˆ€ y : ğ’ª[K], â€–xâ€–â‚Š < â€–yâ€–â‚Š â†’ âˆƒ z âˆˆ t, â€–zâ€–â‚Š = â€–yâ€–â‚Š := by
      intro y hy
      have := ht.ge (Set.mem_univ y)
      simp only [AddSubgroupClass.coe_norm, Set.mem_iUnion, exists_prop', nonempty_prop, U] at this
      obtain âŸ¨z, hz, hz'âŸ© := this
      split_ifs at hz' with h
      Â· simp only [â† coe_lt_coe, coe_nnnorm, AddSubgroupClass.coe_norm] at hy
        simp [hy.not_le] at hz'
      Â· simp only [mem_sphere_iff_norm, sub_zero, AddSubgroupClass.coe_norm] at hz'
        refine âŸ¨z, hz, ?_âŸ©
        simp [â† coe_inj, hz']
    obtain âŸ¨y, _, hy'âŸ© : âˆƒ y : ğ’ª[K], y âˆˆ t âˆ§ â€–yâ€–â‚Š = 1 := by simpa using htm 1 hx'
    obtain âŸ¨w, hwt, hw1, hxwâŸ© : âˆƒ w : ğ’ª[K], w âˆˆ t âˆ§ â€–wâ€–â‚Š < 1 âˆ§ â€–xâ€–â‚Š < â€–wâ€–â‚Š := by
      replace hx' : (âŸ¨_, x, rflâŸ© : Set.range ((â€–Â·â€–â‚Š) : ğ’ª[K] â†’ â„â‰¥0)) < âŸ¨_, 1, rflâŸ© := hx'
      obtain âŸ¨âŸ¨_, w, rflâŸ©, hw, hw'âŸ© := exists_between hx'
      obtain âŸ¨u, hu, hu'âŸ© := htm w hw
      exact âŸ¨u, hu, hu' â–¸ by simpa using hw', hu' â–¸ by simpa using hwâŸ©
    let u := t.filter (fun a â†¦ â€–aâ€–â‚Š < 1)
    have hwu : w âˆˆ u := by simp [u, hwt, hw1]
    obtain âŸ¨l, hl, hl'âŸ© := u.sup'_mem (((â€–Â·â€–â‚Š) : ğ’ª[K] â†’ â„â‰¥0) '' u)
      (fun x hx y hy â†¦ (max_cases x y).elim
        (fun h â†¦ (sup_eq_max (a := x) (b := y) â–¸ h).left.symm â–¸ hx)
        (fun h â†¦ (sup_eq_max (a := x) (b := y) â–¸ h).left.symm â–¸ hy))
      âŸ¨w, hwuâŸ© (â€–Â·â€–â‚Š) (fun _ â†¦ Set.mem_image_of_mem _)
    simp only at hl'
    have hm : (âŸ¨â€–lâ€–â‚Š, l, rflâŸ© : Set.range ((â€–Â·â€–â‚Š) : ğ’ª[K] â†’ â„â‰¥0)) < (âŸ¨1, y, hy'âŸ©) := by
      simp only [Finset.coe_filter, Set.mem_setOf_eq, u] at hl
      simp [hl.right]
    obtain âŸ¨âŸ¨_, m, rflâŸ©, hmâŸ© := exists_between hm
    simp only [Subtype.mk_lt_mk] at hm
    obtain âŸ¨n, hn, hn'âŸ© : âˆƒ n âˆˆ t, â€–nâ€–â‚Š = â€–mâ€–â‚Š := by
      refine htm m (hxw.trans (hm.left.trans_le' ?_))
      rw [hl', Finset.le_sup'_iff]
      exact âŸ¨w, hwu, le_rflâŸ©
    rw [â† hn'] at hm
    refine hm.left.not_le ?_
    rw [hl', Finset.le_sup'_iff]
    refine âŸ¨n, ?_, le_rflâŸ©
    simp [u, hn, hm.right]
  exact {
    __ := hl
    __ := key
    not_a_field' := by
      simp only [ne_eq, Ideal.ext_iff, LocalRing.mem_maximalIdeal, mem_nonunits_iff, Ideal.mem_bot,
        not_forall, isUnit_iff_norm_eq_one]
      refine âŸ¨x, ?_âŸ©
      simp only [â† coe_lt_coe, coe_zero, coe_nnnorm, norm_pos_iff, ne_eq,
        ZeroMemClass.coe_eq_zero, nnnorm_one, coe_one] at hx hx'
      simpa [hx] using hx'.ne
  }

end CompactDVR

lemma compactSpace_iff_completeSpace_and_discreteValuationRing_and_finite_residueField :
    CompactSpace ğ’ª[K] â†” CompleteSpace ğ’ª[K] âˆ§ DiscreteValuationRing ğ’ª[K] âˆ§ Finite ğ“€[K] := by
  refine âŸ¨fun h â†¦ ?_, fun âŸ¨_, _, hâŸ© â†¦ âŸ¨?_âŸ©âŸ©
  Â· have : DiscreteValuationRing ğ’ª[K] := discreteValuationRing_of_compactSpace
    refine âŸ¨complete_of_compact, by assumption, ?_âŸ©
    rw [â† isCompact_univ_iff, isCompact_iff_totallyBounded_isComplete,
        totallyBounded_iff_finite_residueField] at h
    exact h.left
  Â· rw [â† totallyBounded_iff_finite_residueField] at h
    rw [isCompact_iff_totallyBounded_isComplete]
    exact âŸ¨h, completeSpace_iff_isComplete_univ.mp â€¹_â€ºâŸ©

lemma properSpace_iff_compactSpace_integer :
    ProperSpace K â†” CompactSpace ğ’ª[K] := by
  simp only [â† isCompact_univ_iff, Subtype.isCompact_iff, Set.image_univ, Subtype.range_coe_subtype,
             mem_integer_iff', â† mem_closedBall_zero_iff, Set.setOf_mem_eq]
  constructor <;> intro h
  Â· exact isCompact_closedBall 0 1
  Â· suffices LocallyCompactSpace K from .of_nontriviallyNormedField_of_weaklyLocallyCompactSpace K
    exact IsCompact.locallyCompactSpace_of_mem_nhds_of_addGroup h <|
      Metric.closedBall_mem_nhds 0 zero_lt_one

lemma properSpace_iff_completeSpace_and_discreteValuationRing_integer_and_finite_residueField :
    ProperSpace K â†” CompleteSpace K âˆ§ DiscreteValuationRing ğ’ª[K] âˆ§ Finite ğ“€[K] := by
  simp only [properSpace_iff_compactSpace_integer,
      compactSpace_iff_completeSpace_and_discreteValuationRing_and_finite_residueField,
      completeSpace_iff_isComplete_univ (Î± := ğ’ª[K]), Subtype.isComplete_iff,
      NormedField.completeSpace_iff_isComplete_closedBall, Set.image_univ,
      Subtype.range_coe_subtype, mem_integer_iff', â† mem_closedBall_zero_iff, Set.setOf_mem_eq]

end Valued.integer
