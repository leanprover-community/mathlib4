/-
Copyright (c) 2024 Yakov Pechersky. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yakov Pechersky
-/
import Mathlib.Analysis.Normed.Field.Lemmas
import Mathlib.Analysis.Normed.Field.ProperSpace
import Mathlib.RingTheory.DiscreteValuationRing.Basic
import Mathlib.RingTheory.Ideal.IsPrincipalPowQuotient
import Mathlib.RingTheory.Valuation.Archimedean
import Mathlib.Topology.Algebra.Valued.NormedValued
import Mathlib.Topology.Algebra.Valued.ValuedField

/-!
# Necessary and sufficient conditions for a locally compact valued field

## Main Definitions
* `totallyBounded_iff_finite_residueField`: when the valuation ring is a DVR,
  it is totally bounded iff the residue field is finite.

## Tags

norm, nonarchimedean, rank one, compact, locally compact
-/

open NNReal

section NormedField

open scoped NormedField

variable {K : Type*} [NontriviallyNormedField K] [IsUltrametricDist K]

@[simp]
lemma NormedField.v_eq_valuation (x : K) : Valued.v x = NormedField.valuation x := rfl

namespace Valued.integer

-- should we do this all in the Valuation namespace instead?

/-- An element is in the valuation ring if the norm is bounded by 1. This is a variant of
`Valuation.mem_integer_iff`, phrased using norms instead of the valuation. -/
lemma mem_iff {x : K} : x âˆˆ ğ’ª[K] â†” â€–xâ€– â‰¤ 1 := by
  simp [Valuation.mem_integer_iff, â† NNReal.coe_le_coe]

lemma norm_le_one (x : ğ’ª[K]) : â€–xâ€– â‰¤ 1 := mem_iff.mp x.prop

@[simp]
lemma norm_coe_unit (u : ğ’ª[K]Ë£) : â€–((u : ğ’ª[K]) : K)â€– = 1 := by
  simpa [â† NNReal.coe_inj] using
    (Valuation.integer.integers (NormedField.valuation (K := K))).valuation_unit u

lemma norm_unit (u : ğ’ª[K]Ë£) : â€–(u : ğ’ª[K])â€– = 1 := by
  simp

lemma isUnit_iff_norm_eq_one {u : ğ’ª[K]} : IsUnit u â†” â€–uâ€– = 1 := by
  simpa [â† NNReal.coe_inj] using
    (Valuation.integer.integers (NormedField.valuation (K := K))).isUnit_iff_valuation_eq_one

lemma norm_irreducible_lt_one {Ï– : ğ’ª[K]} (h : Irreducible Ï–) : â€–Ï–â€– < 1 :=
  Valuation.integer.v_irreducible_lt_one h

lemma norm_irreducible_pos {Ï– : ğ’ª[K]} (h : Irreducible Ï–) : 0 < â€–Ï–â€– :=
  Valuation.integer.v_irreducible_pos h

lemma coe_span_singleton_eq_closedBall (x : ğ’ª[K]) :
    (Ideal.span {x} : Set ğ’ª[K]) = Metric.closedBall 0 â€–xâ€– := by
  simp [Valuation.integer.coe_span_singleton_eq_setOf_le_v_coe, Set.ext_iff, â† NNReal.coe_le_coe]

lemma _root_.Irreducible.maximalIdeal_eq_closedBall [IsDiscreteValuationRing ğ’ª[K]]
    {Ï– : ğ’ª[K]} (h : Irreducible Ï–) :
    (ğ“‚[K] : Set ğ’ª[K]) = Metric.closedBall 0 â€–Ï–â€– := by
  simp [h.maximalIdeal_eq_setOf_le_v_coe, Set.ext_iff, â† NNReal.coe_le_coe]

lemma _root_.Irreducible.maximalIdeal_pow_eq_closedBall_pow [IsDiscreteValuationRing ğ’ª[K]]
    {Ï– : ğ’ª[K]} (h : Irreducible Ï–) (n : â„•) :
    ((ğ“‚[K] ^ n : Ideal ğ’ª[K]) : Set ğ’ª[K]) = Metric.closedBall 0 (â€–Ï–â€– ^ n) := by
  simp [h.maximalIdeal_pow_eq_setOf_le_v_coe_pow, Set.ext_iff, â† NNReal.coe_le_coe]

variable (K) in
lemma exists_norm_coe_lt_one : âˆƒ x : ğ’ª[K], 0 < â€–(x : K)â€– âˆ§ â€–(x : K)â€– < 1 := by
  obtain âŸ¨x, hx, hx'âŸ© := NormedField.exists_norm_lt_one K
  refine âŸ¨âŸ¨x, hx'.leâŸ©, ?_âŸ©
  simpa [hx', Subtype.ext_iff] using hx

variable (K) in
lemma exists_norm_lt_one : âˆƒ x : ğ’ª[K], 0 < â€–xâ€– âˆ§ â€–xâ€– < 1 :=
  exists_norm_coe_lt_one K

variable (K) in
lemma exists_nnnorm_lt_one : âˆƒ x : ğ’ª[K], 0 < â€–xâ€–â‚Š âˆ§ â€–xâ€–â‚Š < 1 :=
  exists_norm_coe_lt_one K

end Valued.integer

end NormedField

namespace Valued.integer

variable {K Î“â‚€ : Type*} [Field K] [LinearOrderedCommGroupWithZero Î“â‚€] [Valued K Î“â‚€]

section FiniteResidueField

open Valued

lemma finite_quotient_maximalIdeal_pow_of_finite_residueField [IsDiscreteValuationRing ğ’ª[K]]
    (h : Finite ğ“€[K]) (n : â„•) :
    Finite (ğ’ª[K] â§¸ ğ“‚[K] ^ n) := by
  induction n with
  | zero =>
    simp only [pow_zero, Ideal.one_eq_top]
    exact Finite.of_fintype (â†¥ğ’ª[K] â§¸ âŠ¤)
  | succ n ih =>
    have : ğ“‚[K] ^ (n + 1) â‰¤ ğ“‚[K] ^ n := Ideal.pow_le_pow_right (by simp)
    replace ih := Finite.of_equiv _ (DoubleQuot.quotQuotEquivQuotOfLE this).symm.toEquiv
    suffices Finite (Ideal.map (Ideal.Quotient.mk (ğ“‚[K] ^ (n + 1))) (ğ“‚[K] ^ n)) from
      Finite.of_finite_quot_finite_ideal
        (I := Ideal.map (Ideal.Quotient.mk _) (ğ“‚[K] ^ n))
    exact @Finite.of_equiv _ _ h
      ((Ideal.quotEquivPowQuotPowSuccEquiv (IsPrincipalIdealRing.principal ğ“‚[K])
        (IsDiscreteValuationRing.not_a_field _) n).trans
        (Ideal.powQuotPowSuccEquivMapMkPowSuccPow _ n))

open scoped Valued
lemma totallyBounded_iff_finite_residueField [(Valued.v : Valuation K Î“â‚€).RankOne]
    [IsDiscreteValuationRing ğ’ª[K]] :
    TotallyBounded (Set.univ (Î± := ğ’ª[K])) â†” Finite ğ“€[K] := by
  constructor
  Â· intro H
    obtain âŸ¨p, hpâŸ© := IsDiscreteValuationRing.exists_irreducible ğ’ª[K]
    have := Metric.finite_approx_of_totallyBounded H â€–pâ€– (norm_pos_iff.mpr hp.ne_zero)
    simp only [Set.subset_univ, Set.univ_subset_iff, true_and] at this
    obtain âŸ¨t, ht, ht'âŸ© := this
    rw [â† Set.finite_univ_iff]
    refine (ht.image (IsLocalRing.residue _)).subset ?_
    rintro âŸ¨xâŸ©
    replace ht' := ht'.ge (Set.mem_univ x)
    simp only [Set.mem_iUnion, Metric.mem_ball, exists_prop] at ht'
    obtain âŸ¨y, hy, hy'âŸ© := ht'
    simp only [Submodule.Quotient.quot_mk_eq_mk, Ideal.Quotient.mk_eq_mk, Set.mem_univ,
      IsLocalRing.residue, Set.mem_image, true_implies]
    refine âŸ¨y, hy, ?_âŸ©
    convert (Ideal.Quotient.mk_eq_mk_iff_sub_mem (I := ğ“‚[K]) y x).mpr _
    -- TODO: make Valued.maximalIdeal abbreviations instead of def
    rw [Valued.maximalIdeal, hp.maximalIdeal_eq, â† SetLike.mem_coe,
      (Valuation.integer.integers _).coe_span_singleton_eq_setOf_le_v_algebraMap]
    rw [dist_comm] at hy'
    simpa [dist_eq_norm] using hy'.le
  Â· intro H
    rw [Metric.totallyBounded_iff]
    intro Îµ Îµpos
    obtain âŸ¨p, hpâŸ© := IsDiscreteValuationRing.exists_irreducible ğ’ª[K]
    have hp' := Valuation.integer.v_irreducible_lt_one hp
    obtain âŸ¨n, hnâŸ© : âˆƒ n : â„•, â€–(p : K)â€– ^ n < Îµ := exists_pow_lt_of_lt_one Îµpos
      (toNormedField.norm_lt_one_iff.mpr hp')
    have hF := finite_quotient_maximalIdeal_pow_of_finite_residueField H n
    refine âŸ¨Quotient.out '' (Set.univ (Î± := ğ’ª[K] â§¸ (ğ“‚[K] ^ n))), Set.toFinite _, ?_âŸ©
    have : {y : ğ’ª[K] | v (y : K) â‰¤ v (p : K) ^ n} = Metric.closedBall 0 (â€–pâ€– ^ n)  := by
      ext
      simp [â† norm_pow]
    simp only [Ideal.univ_eq_iUnion_image_add (ğ“‚[K] ^ n), hp.maximalIdeal_pow_eq_setOf_le_v_coe_pow,
      this, AddSubgroupClass.coe_norm, Set.image_univ, Set.mem_range, Set.iUnion_exists,
      Set.iUnion_iUnion_eq', Set.iUnion_subset_iff, Metric.vadd_closedBall, vadd_eq_add, add_zero]
    intro
    exact (Metric.closedBall_subset_ball hn).trans (Set.subset_iUnion_of_subset _ le_rfl)

end FiniteResidueField

section CompactDVR

open Valued

lemma locallyFiniteOrder_units_mrange_of_isCompact_integer (hc : IsCompact (X := K) ğ’ª[K]) :
    Nonempty (LocallyFiniteOrder (MonoidHom.mrange (Valued.v : Valuation K Î“â‚€))Ë£):= by
  -- TODO: generalize to `Valuation.Integer`, which will require showing that `IsCompact`
  -- pulls back across `TopologicalSpace.induced` from a `LocallyCompactSpace`.
  constructor
  refine LocallyFiniteOrder.ofFiniteIcc ?_
  -- We only need to show that we can construct a finite set for some set between
  -- a non-zero `z : Î“â‚€` and 1, because we can scale/invert this set to cover the whole group.
  suffices âˆ€ z : (MonoidHom.mrange (Valued.v : Valuation K Î“â‚€))Ë£, (Set.Icc z 1).Finite by
    rintro x y
    rcases lt_trichotomy y x with hxy | rfl | hxy
    Â· rw [Set.Icc_eq_empty_of_lt]
      Â· exact Set.finite_empty
      Â· simp [hxy]
    Â· simp
    wlog h : x â‰¤ 1 generalizing x y
    Â· push_neg at h
      specialize this yâ»Â¹ xâ»Â¹ (inv_lt_inv' hxy) (inv_le_one_of_one_le (h.trans hxy).le)
      refine (this.inv).subset ?_
      rw [Set.inv_Icc]
      intro
      simp +contextual
    generalize_proofs _ _ _ _ hxu hyu
    rcases le_total y 1 with hy | hy
    Â· exact (this x).subset (Set.Icc_subset_Icc_right hy)
    Â· have H : (Set.Icc yâ»Â¹ 1).Finite := this _
      refine ((this x).union H.inv).subset (le_of_eq ?_)
      rw [Set.inv_Icc, inv_one, Set.Icc_union_Icc_eq_Icc] <;>
      simp [h, hy]
  -- We can construct a family of spheres at every single element of the valuation ring
  -- outside of a closed ball, which will cover.
  -- Since we are in a compact space, this cover has a finite subcover.
  -- First, we need to pick a threshold element with a nontrivial valuation less than 1,
  -- which will form -- the inner closed ball of the cover, which we need to cover 0.
  intro z
  obtain âŸ¨a, haâŸ© := z.val.prop
  rcases lt_or_ge 1 z with hz1 | hz1
  Â· rw [Set.Icc_eq_empty_of_lt]
    Â· exact Set.finite_empty
    Â· simp [hz1]
  have z0' : 0 < (z : MonoidHom.mrange (Valued.v : Valuation K Î“â‚€)) := by simp
  have z0 : 0 < ((z : MonoidHom.mrange (Valued.v : Valuation K Î“â‚€)) : Î“â‚€) :=
    Subtype.coe_lt_coe.mpr z0'
  have a0 : 0 < v a := by simp [ha, z0]
  -- Construct our cover, which has an inner closed ball, and spheres for each element
  -- outside of the closed ball. These are all open sets by the nonarchimedean property.
  let U : K â†’ Set K := fun y â†¦ if v (y : K) â‰¤ z
    then {w | v (w : K) â‰¤ z}
    else {w | v (w : K) = v (y : K)}
  have := hc.elim_finite_subcover U
  specialize this ?_ ?_
  Â· intro w
    simp only [U]
    split_ifs with hw
    Â· exact Valued.isOpen_closedball _ z0.ne'
    Â· refine Valued.isOpen_sphere _ ?_
      push_neg at hw
      refine (hw.trans' ?_).ne'
      simp [z0]
  Â· intro w
    simp only [integer, SetLike.mem_coe, Valuation.mem_integer_iff, Set.mem_iUnion, U]
    intro hw
    use if v w â‰¤ z then a else w
    split_ifs <;>
    simp_all
  -- For each element of the valuation ring that is bigger than our threshold element above,
  -- there must be something in the cover that has the precise valuation of the element,
  -- because it must be outside the inner closed ball, and thus is covered by some sphere.
  obtain âŸ¨t, htâŸ© := this
  classical
  refine (t.finite_toSet.dependent_image ?_).subset ?_
  Â· refine fun i hi â†¦ if hi' : v i â‰¤ z then z else Units.mk0 âŸ¨(v i), by simpâŸ© ?_
    push_neg at hi'
    exact Subtype.coe_injective.ne_iff.mp (hi'.trans' z0).ne'
  Â· intro i
    simp only [Set.mem_Icc, Finset.mem_coe, exists_prop, Set.mem_setOf_eq, and_imp]
    -- we get the `c` from the cover that covers our arbitrary `i` with its set
    obtain âŸ¨c, hcâŸ© := i.val.prop
    intro hzi hi1
    have hj := ht (hc.trans_le hi1)
    simp only [Set.mem_iUnion, exists_prop, U] at hj
    obtain âŸ¨j, hj, hj'âŸ© := hj
    use j, hj
    -- and this `c` is either less than or greater than (or equal to) the threshold element
    split_ifs at hj' with hcj
    Â· simp only [Set.mem_setOf_eq, hc, Subtype.coe_le_coe, Units.val_le_val] at hj'
      simp [hcj, le_antisymm hj' hzi]
    Â· simp only [Set.mem_setOf_eq] at hj'
      rw [dif_neg hcj]
      simp [â† hj', hc]

lemma mulArchimedean_mrange_of_isCompact_integer (hc : IsCompact (X := K) ğ’ª[K]) :
    MulArchimedean (MonoidHom.mrange (Valued.v : Valuation K Î“â‚€)) := by
  rw [â† Units.mulArchimedean_iff]
  obtain âŸ¨_âŸ© := locallyFiniteOrder_units_mrange_of_isCompact_integer hc
  exact MulArchimedean.of_locallyFiniteOrder

lemma isPrincipalIdealRing_of_compactSpace [hc : CompactSpace ğ’ª[K]] :
    IsPrincipalIdealRing ğ’ª[K] := by
  -- The strategy to show that we have a PIR is by contradiction,
  -- assuming that the range of the valuation is densely ordered.
  have hi : Valuation.Integers (R := K) Valued.v ğ’ª[K] := Valuation.integer.integers v
  have hc : IsCompact (X := K) ğ’ª[K] := isCompact_iff_compactSpace.mpr hc
  -- We can also construct that it has a locally finite order, by compactness
  -- which leads to a contradiction.
  obtain âŸ¨_âŸ© := locallyFiniteOrder_units_mrange_of_isCompact_integer hc
  have hm := mulArchimedean_mrange_of_isCompact_integer hc
  -- The key result is that a valuation ring that maps into a `MulArchimedean` value group
  -- is a PIR iff the value group is not densely ordered.
  refine hi.isPrincipalIdealRing_iff_not_denselyOrdered_mrange.mpr fun _ â†¦ ?_
  -- since we are densely ordered, we necessarily are nontrivial
  exact not_subsingleton (MonoidHom.mrange (v : Valuation K Î“â‚€))Ë£
    (LocallyFiniteOrder.denselyOrdered_iff_subsingleton.mp inferInstance)

theorem _root_.Valuation.isNontrivial_iff_not_a_field {K Î“ : Type*} [Field K]
    [LinearOrderedCommGroupWithZero Î“] (v : Valuation K Î“) :
    v.IsNontrivial â†” IsLocalRing.maximalIdeal v.integer â‰  âŠ¥ := by
  simp_rw [ne_eq, eq_bot_iff, v.isNontrivial_iff_exists_lt_one, SetLike.le_def, Ideal.mem_bot,
    not_forall, exists_prop, IsLocalRing.notMem_maximalIdeal.not_right,
    Valuation.Integer.not_isUnit_iff_valuation_lt_one]
  exact âŸ¨fun âŸ¨x, hx0, hx1âŸ© â†¦ âŸ¨âŸ¨x, hx1.leâŸ©, by simp [Subtype.ext_iff, *]âŸ©,
  fun âŸ¨x, hx1, hx0âŸ© â†¦ âŸ¨x, by simp [*]âŸ©âŸ©

lemma isDiscreteValuationRing_of_compactSpace [hn : (Valued.v : Valuation K Î“â‚€).IsNontrivial]
    [CompactSpace ğ’ª[K]] : IsDiscreteValuationRing ğ’ª[K] where
  -- To prove we have a DVR, we need to show it is
  -- a local ring (instance is directly inferred) and a PIR and not a field.
  __ := isPrincipalIdealRing_of_compactSpace
  not_a_field' := v.isNontrivial_iff_not_a_field.mp hn

end CompactDVR

lemma compactSpace_iff_completeSpace_and_isDiscreteValuationRing_and_finite_residueField
    [(Valued.v : Valuation K Î“â‚€).RankOne] :
    CompactSpace ğ’ª[K] â†” CompleteSpace ğ’ª[K] âˆ§ IsDiscreteValuationRing ğ’ª[K] âˆ§ Finite ğ“€[K] := by
  refine âŸ¨fun h â†¦ ?_, fun âŸ¨_, _, hâŸ© â†¦ âŸ¨?_âŸ©âŸ©
  Â· have : IsDiscreteValuationRing ğ’ª[K] := isDiscreteValuationRing_of_compactSpace
    refine âŸ¨complete_of_compact, by assumption, ?_âŸ©
    rw [â† isCompact_univ_iff, isCompact_iff_totallyBounded_isComplete,
        totallyBounded_iff_finite_residueField] at h
    exact h.left
  Â· rw [â† totallyBounded_iff_finite_residueField] at h
    rw [isCompact_iff_totallyBounded_isComplete]
    exact âŸ¨h, completeSpace_iff_isComplete_univ.mp â€¹_â€ºâŸ©

lemma properSpace_iff_compactSpace_integer [(Valued.v : Valuation K Î“â‚€).RankOne] :
    ProperSpace K â†” CompactSpace ğ’ª[K] := by
  simp only [â† isCompact_univ_iff, Subtype.isCompact_iff, Set.image_univ, Subtype.range_coe_subtype,
             toNormedField.setOf_mem_integer_eq_closedBall]
  constructor <;> intro h
  Â· exact isCompact_closedBall 0 1
  Â· suffices LocallyCompactSpace K from .of_nontriviallyNormedField_of_weaklyLocallyCompactSpace K
    exact IsCompact.locallyCompactSpace_of_mem_nhds_of_addGroup h <|
      Metric.closedBall_mem_nhds 0 zero_lt_one

lemma properSpace_iff_completeSpace_and_isDiscreteValuationRing_integer_and_finite_residueField
    [(Valued.v : Valuation K Î“â‚€).RankOne] :
    ProperSpace K â†” CompleteSpace K âˆ§ IsDiscreteValuationRing ğ’ª[K] âˆ§ Finite ğ“€[K] := by
  simp only [properSpace_iff_compactSpace_integer,
      compactSpace_iff_completeSpace_and_isDiscreteValuationRing_and_finite_residueField,
      toNormedField.setOf_mem_integer_eq_closedBall,
      completeSpace_iff_isComplete_univ (Î± := ğ’ª[K]), Subtype.isComplete_iff,
      NormedField.completeSpace_iff_isComplete_closedBall, Set.image_univ,
      Subtype.range_coe_subtype]

end Valued.integer
