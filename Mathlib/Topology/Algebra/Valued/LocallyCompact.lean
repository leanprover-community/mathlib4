/-
Copyright (c) 2024 Yakov Pechersky. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yakov Pechersky
-/
import Mathlib.RingTheory.DiscreteValuationRing.Basic
import Mathlib.RingTheory.Ideal.IsPrincipalPowQuotient
import Mathlib.Topology.Algebra.Valued.NormedValued
import Mathlib.Topology.Algebra.Valued.ValuedField

/-!
# Necessary and sufficient conditions for a locally compact nonarchimedean normed field

## Main Definitions
* `totallyBounded_iff_finite_residueField`: when the valuation ring is a DVR,
  it is totally bounded iff the residue field is finite.

## Tags

norm, nonarchimedean, rank one, compact, locally compact
-/

variable {K : Type*} [NontriviallyNormedField K] [IsUltrametricDist K]

open NNReal

open scoped NormedField

@[simp]
lemma NormedField.v_eq_valuation (x : K) : Valued.v x = NormedField.valuation x := rfl

namespace Valued.integer

-- should we do this all in the Valuation namespace instead?

/-- An element is in the valuation ring if the norm is bounded by 1. This is a variant of
`Valuation.mem_integer_iff`, phrased using norms instead of the valuation. -/
lemma mem_iff {x : K} : x âˆˆ ğ’ª[K] â†” â€–xâ€– â‰¤ 1 := by
  simp [Valuation.mem_integer_iff, â† NNReal.coe_le_coe]

lemma norm_le_one (x : ğ’ª[K]) : â€–xâ€– â‰¤ 1 := mem_iff.mp x.prop

@[simp]
lemma norm_coe_unit (u : ğ’ª[K]Ë£) : â€–((u : ğ’ª[K]) : K)â€– = 1 := by
  simpa [â† NNReal.coe_inj] using
    (Valuation.integer.integers (NormedField.valuation (K := K))).valuation_unit u

lemma norm_unit (u : ğ’ª[K]Ë£) : â€–(u : ğ’ª[K])â€– = 1 := by
  simp

lemma isUnit_iff_norm_eq_one {u : ğ’ª[K]} : IsUnit u â†” â€–uâ€– = 1 := by
  simpa [â† NNReal.coe_inj] using
    (Valuation.integer.integers (NormedField.valuation (K := K))).isUnit_iff_valuation_eq_one

lemma norm_irreducible_lt_one {Ï– : ğ’ª[K]} (h : Irreducible Ï–) : â€–Ï–â€– < 1 :=
  lt_of_le_of_ne (norm_le_one Ï–) (mt isUnit_iff_norm_eq_one.mpr h.not_unit)

lemma norm_irreducible_pos {Ï– : ğ’ª[K]} (h : Irreducible Ï–) : 0 < â€–Ï–â€– :=
  lt_of_le_of_ne (_root_.norm_nonneg Ï–) (by simp [eq_comm, h.ne_zero])

lemma coe_span_singleton_eq_closedBall (x : ğ’ª[K]) :
    (Ideal.span {x} : Set ğ’ª[K]) = Metric.closedBall 0 â€–xâ€– := by
  rcases eq_or_ne x 0 with rfl|hx
  Â· simp [Set.singleton_zero, Ideal.span_zero]
  ext y
  simp only [SetLike.mem_coe, Ideal.mem_span_singleton', AddSubgroupClass.coe_norm,
    Metric.mem_closedBall, dist_zero_right]
  constructor
  Â· rintro âŸ¨z, rflâŸ©
    simpa using mul_le_mul_of_nonneg_right (norm_le_one z) (_root_.norm_nonneg x)
  Â· intro h
    refine âŸ¨âŸ¨y / x, ?_âŸ©, ?_âŸ©
    Â· simpa [mem_iff] using div_le_one_of_leâ‚€ h (_root_.norm_nonneg _)
    Â· simpa only [Subtype.ext_iff] using div_mul_cancelâ‚€ (y : K) (by simpa using hx)

lemma _root_.Irreducible.maximalIdeal_eq_closedBall [IsDiscreteValuationRing ğ’ª[K]]
    {Ï– : ğ’ª[K]} (h : Irreducible Ï–) :
    (ğ“‚[K] : Set ğ’ª[K]) = Metric.closedBall 0 â€–Ï–â€– := by
  rw [â† coe_span_singleton_eq_closedBall, â† h.maximalIdeal_eq]

lemma _root_.Irreducible.maximalIdeal_pow_eq_closedBall_pow [IsDiscreteValuationRing ğ’ª[K]]
    {Ï– : ğ’ª[K]} (h : Irreducible Ï–) (n : â„•) :
    ((ğ“‚[K] ^ n : Ideal ğ’ª[K]) : Set ğ’ª[K]) = Metric.closedBall 0 (â€–Ï–â€– ^ n) := by
  have : â€–Ï–â€– ^ n = â€–Ï– ^ nâ€– := by simp
  rw [this, â† coe_span_singleton_eq_closedBall, â† Ideal.span_singleton_pow, â† h.maximalIdeal_eq]

section FiniteResidueField

variable {K : Type*} [NontriviallyNormedField K] [IsUltrametricDist K]

open Valued

lemma finite_quotient_maximalIdeal_pow_of_finite_residueField [IsDiscreteValuationRing ğ’ª[K]]
    (h : Finite ğ“€[K]) (n : â„•) :
    Finite (ğ’ª[K] â§¸ ğ“‚[K] ^ n) := by
  induction n with
  | zero =>
    simp only [pow_zero, Ideal.one_eq_top]
    exact Finite.of_fintype (â†¥ğ’ª[K] â§¸ âŠ¤)
  | succ n ih =>
    have : ğ“‚[K] ^ (n + 1) â‰¤ ğ“‚[K] ^ n := Ideal.pow_le_pow_right (by simp)
    replace ih := Finite.of_equiv _ (DoubleQuot.quotQuotEquivQuotOfLE this).symm.toEquiv
    suffices Finite (Ideal.map (Ideal.Quotient.mk (ğ“‚[K] ^ (n + 1))) (ğ“‚[K] ^ n)) from
      Finite.of_finite_quot_finite_ideal
        (I := Ideal.map (Ideal.Quotient.mk _) (ğ“‚[K] ^ n))
    exact @Finite.of_equiv _ _ h
      ((Ideal.quotEquivPowQuotPowSuccEquiv (IsPrincipalIdealRing.principal ğ“‚[K])
        (IsDiscreteValuationRing.not_a_field _) n).trans
        (Ideal.powQuotPowSuccEquivMapMkPowSuccPow _ n))

lemma totallyBounded_iff_finite_residueField [IsDiscreteValuationRing ğ’ª[K]] :
    TotallyBounded (Set.univ (Î± := ğ’ª[K])) â†” Finite ğ“€[K] := by
  constructor
  Â· intro H
    obtain âŸ¨p, hpâŸ© := IsDiscreteValuationRing.exists_irreducible ğ’ª[K]
    have := Metric.finite_approx_of_totallyBounded H â€–pâ€– (norm_pos_iff.mpr hp.ne_zero)
    simp only [Set.subset_univ, Set.univ_subset_iff, true_and] at this
    obtain âŸ¨t, ht, ht'âŸ© := this
    rw [â† Set.finite_univ_iff]
    refine (ht.image (IsLocalRing.residue _)).subset ?_
    rintro âŸ¨xâŸ©
    replace ht' := ht'.ge (Set.mem_univ x)
    simp only [Set.mem_iUnion, Metric.mem_ball, exists_prop] at ht'
    obtain âŸ¨y, hy, hy'âŸ© := ht'
    simp only [Submodule.Quotient.quot_mk_eq_mk, Ideal.Quotient.mk_eq_mk, Set.mem_univ,
      IsLocalRing.residue, Set.mem_image, true_implies]
    refine âŸ¨y, hy, ?_âŸ©
    convert (Ideal.Quotient.mk_eq_mk_iff_sub_mem (I := ğ“‚[K]) y x).mpr _
    -- TODO: make Valued.maximalIdeal abbreviations instead of def
    rw [Valued.maximalIdeal, hp.maximalIdeal_eq, â† SetLike.mem_coe,
      coe_span_singleton_eq_closedBall]
    rw [dist_comm] at hy'
    simpa [dist_eq_norm] using hy'.le
  Â· intro H
    rw [Metric.totallyBounded_iff]
    intro Îµ Îµpos
    obtain âŸ¨p, hpâŸ© := IsDiscreteValuationRing.exists_irreducible ğ’ª[K]
    have hp' := norm_irreducible_lt_one hp
    obtain âŸ¨n, hnâŸ© : âˆƒ n : â„•, â€–pâ€– ^ n < Îµ := exists_pow_lt_of_lt_one Îµpos hp'
    have hF := finite_quotient_maximalIdeal_pow_of_finite_residueField H n
    refine âŸ¨Quotient.out '' (Set.univ (Î± := ğ’ª[K] â§¸ (ğ“‚[K] ^ n))), Set.toFinite _, ?_âŸ©
    simp only [Ideal.univ_eq_iUnion_image_add (ğ“‚[K] ^ n), hp.maximalIdeal_pow_eq_closedBall_pow,
      AddSubgroupClass.coe_norm, Set.image_add_left, preimage_add_closedBall, sub_neg_eq_add,
      zero_add, Set.image_univ, Set.mem_range, Set.iUnion_exists, Set.iUnion_iUnion_eq',
      Set.iUnion_subset_iff, Metric.vadd_closedBall, vadd_eq_add, add_zero]
    intro
    exact (Metric.closedBall_subset_ball hn).trans (Set.subset_iUnion_of_subset _ le_rfl)

end FiniteResidueField

end Valued.integer
