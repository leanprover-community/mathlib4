/-
Copyright (c) 2024 Yakov Pechersky. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yakov Pechersky
-/
import Mathlib.Analysis.Normed.Field.Lemmas
import Mathlib.Analysis.Normed.Field.ProperSpace
import Mathlib.RingTheory.DiscreteValuationRing.Basic
import Mathlib.RingTheory.Ideal.IsPrincipalPowQuotient
import Mathlib.RingTheory.Valuation.Archimedean
import Mathlib.Topology.Algebra.Valued.NormedValued
import Mathlib.Topology.Algebra.Valued.ValuedField

/-!
# Necessary and sufficient conditions for a locally compact nonarchimedean normed field

## Main Definitions
* `totallyBounded_iff_finite_residueField`: when the valuation ring is a DVR,
  it is totally bounded iff the residue field is finite.

## Tags

norm, nonarchimedean, rank one, compact, locally compact
-/

variable {K : Type*} [NontriviallyNormedField K] [IsUltrametricDist K]

open NNReal

open scoped NormedField

@[simp]
lemma NormedField.v_eq_valuation (x : K) : Valued.v x = NormedField.valuation x := rfl

namespace Valued.integer

-- should we do this all in the Valuation namespace instead?

/-- An element is in the valuation ring if the norm is bounded by 1. This is a variant of
`Valuation.mem_integer_iff`, phrased using norms instead of the valuation. -/
lemma mem_iff {x : K} : x âˆˆ ğ’ª[K] â†” â€–xâ€– â‰¤ 1 := by
  simp [Valuation.mem_integer_iff, â† NNReal.coe_le_coe]

lemma norm_le_one (x : ğ’ª[K]) : â€–xâ€– â‰¤ 1 := mem_iff.mp x.prop

@[simp]
lemma norm_coe_unit (u : ğ’ª[K]Ë£) : â€–((u : ğ’ª[K]) : K)â€– = 1 := by
  simpa [â† NNReal.coe_inj] using
    (Valuation.integer.integers (NormedField.valuation (K := K))).valuation_unit u

lemma norm_unit (u : ğ’ª[K]Ë£) : â€–(u : ğ’ª[K])â€– = 1 := by
  simp

lemma isUnit_iff_norm_eq_one {u : ğ’ª[K]} : IsUnit u â†” â€–uâ€– = 1 := by
  simpa [â† NNReal.coe_inj] using
    (Valuation.integer.integers (NormedField.valuation (K := K))).isUnit_iff_valuation_eq_one

lemma norm_irreducible_lt_one {Ï– : ğ’ª[K]} (h : Irreducible Ï–) : â€–Ï–â€– < 1 :=
  lt_of_le_of_ne (norm_le_one Ï–) (mt isUnit_iff_norm_eq_one.mpr h.not_isUnit)

lemma norm_irreducible_pos {Ï– : ğ’ª[K]} (h : Irreducible Ï–) : 0 < â€–Ï–â€– :=
  lt_of_le_of_ne (_root_.norm_nonneg Ï–) (by simp [eq_comm, h.ne_zero])

lemma coe_span_singleton_eq_closedBall (x : ğ’ª[K]) :
    (Ideal.span {x} : Set ğ’ª[K]) = Metric.closedBall 0 â€–xâ€– := by
  rcases eq_or_ne x 0 with rfl|hx
  Â· simp [Set.singleton_zero, Ideal.span_zero]
  ext y
  simp only [SetLike.mem_coe, Ideal.mem_span_singleton', AddSubgroupClass.coe_norm,
    Metric.mem_closedBall, dist_zero_right]
  constructor
  Â· rintro âŸ¨z, rflâŸ©
    simpa using mul_le_mul_of_nonneg_right (norm_le_one z) (_root_.norm_nonneg x)
  Â· intro h
    refine âŸ¨âŸ¨y / x, ?_âŸ©, ?_âŸ©
    Â· simpa [mem_iff] using div_le_one_of_leâ‚€ h (_root_.norm_nonneg _)
    Â· simpa only [Subtype.ext_iff] using div_mul_cancelâ‚€ (y : K) (by simpa using hx)

lemma _root_.Irreducible.maximalIdeal_eq_closedBall [IsDiscreteValuationRing ğ’ª[K]]
    {Ï– : ğ’ª[K]} (h : Irreducible Ï–) :
    (ğ“‚[K] : Set ğ’ª[K]) = Metric.closedBall 0 â€–Ï–â€– := by
  rw [â† coe_span_singleton_eq_closedBall, â† h.maximalIdeal_eq]

lemma _root_.Irreducible.maximalIdeal_pow_eq_closedBall_pow [IsDiscreteValuationRing ğ’ª[K]]
    {Ï– : ğ’ª[K]} (h : Irreducible Ï–) (n : â„•) :
    ((ğ“‚[K] ^ n : Ideal ğ’ª[K]) : Set ğ’ª[K]) = Metric.closedBall 0 (â€–Ï–â€– ^ n) := by
  have : â€–Ï–â€– ^ n = â€–Ï– ^ nâ€– := by simp
  rw [this, â† coe_span_singleton_eq_closedBall, â† Ideal.span_singleton_pow, â† h.maximalIdeal_eq]

section FiniteResidueField

variable {K : Type*} [NontriviallyNormedField K] [IsUltrametricDist K]

open Valued

lemma finite_quotient_maximalIdeal_pow_of_finite_residueField [IsDiscreteValuationRing ğ’ª[K]]
    (h : Finite ğ“€[K]) (n : â„•) :
    Finite (ğ’ª[K] â§¸ ğ“‚[K] ^ n) := by
  induction n with
  | zero =>
    simp only [pow_zero, Ideal.one_eq_top]
    exact Finite.of_fintype (â†¥ğ’ª[K] â§¸ âŠ¤)
  | succ n ih =>
    have : ğ“‚[K] ^ (n + 1) â‰¤ ğ“‚[K] ^ n := Ideal.pow_le_pow_right (by simp)
    replace ih := Finite.of_equiv _ (DoubleQuot.quotQuotEquivQuotOfLE this).symm.toEquiv
    suffices Finite (Ideal.map (Ideal.Quotient.mk (ğ“‚[K] ^ (n + 1))) (ğ“‚[K] ^ n)) from
      Finite.of_finite_quot_finite_ideal
        (I := Ideal.map (Ideal.Quotient.mk _) (ğ“‚[K] ^ n))
    exact @Finite.of_equiv _ _ h
      ((Ideal.quotEquivPowQuotPowSuccEquiv (IsPrincipalIdealRing.principal ğ“‚[K])
        (IsDiscreteValuationRing.not_a_field _) n).trans
        (Ideal.powQuotPowSuccEquivMapMkPowSuccPow _ n))

lemma totallyBounded_iff_finite_residueField [IsDiscreteValuationRing ğ’ª[K]] :
    TotallyBounded (Set.univ (Î± := ğ’ª[K])) â†” Finite ğ“€[K] := by
  constructor
  Â· intro H
    obtain âŸ¨p, hpâŸ© := IsDiscreteValuationRing.exists_irreducible ğ’ª[K]
    have := Metric.finite_approx_of_totallyBounded H â€–pâ€– (norm_pos_iff.mpr hp.ne_zero)
    simp only [Set.subset_univ, Set.univ_subset_iff, true_and] at this
    obtain âŸ¨t, ht, ht'âŸ© := this
    rw [â† Set.finite_univ_iff]
    refine (ht.image (IsLocalRing.residue _)).subset ?_
    rintro âŸ¨xâŸ©
    replace ht' := ht'.ge (Set.mem_univ x)
    simp only [Set.mem_iUnion, Metric.mem_ball, exists_prop] at ht'
    obtain âŸ¨y, hy, hy'âŸ© := ht'
    simp only [Submodule.Quotient.quot_mk_eq_mk, Ideal.Quotient.mk_eq_mk, Set.mem_univ,
      IsLocalRing.residue, Set.mem_image, true_implies]
    refine âŸ¨y, hy, ?_âŸ©
    convert (Ideal.Quotient.mk_eq_mk_iff_sub_mem (I := ğ“‚[K]) y x).mpr _
    -- TODO: make Valued.maximalIdeal abbreviations instead of def
    rw [Valued.maximalIdeal, hp.maximalIdeal_eq, â† SetLike.mem_coe,
      coe_span_singleton_eq_closedBall]
    rw [dist_comm] at hy'
    simpa [dist_eq_norm] using hy'.le
  Â· intro H
    rw [Metric.totallyBounded_iff]
    intro Îµ Îµpos
    obtain âŸ¨p, hpâŸ© := IsDiscreteValuationRing.exists_irreducible ğ’ª[K]
    have hp' := norm_irreducible_lt_one hp
    obtain âŸ¨n, hnâŸ© : âˆƒ n : â„•, â€–pâ€– ^ n < Îµ := exists_pow_lt_of_lt_one Îµpos hp'
    have hF := finite_quotient_maximalIdeal_pow_of_finite_residueField H n
    refine âŸ¨Quotient.out '' (Set.univ (Î± := ğ’ª[K] â§¸ (ğ“‚[K] ^ n))), Set.toFinite _, ?_âŸ©
    simp only [Ideal.univ_eq_iUnion_image_add (ğ“‚[K] ^ n), hp.maximalIdeal_pow_eq_closedBall_pow,
      AddSubgroupClass.coe_norm, Set.image_add_left, preimage_add_closedBall, sub_neg_eq_add,
      zero_add, Set.image_univ, Set.mem_range, Set.iUnion_exists, Set.iUnion_iUnion_eq',
      Set.iUnion_subset_iff, Metric.vadd_closedBall, vadd_eq_add, add_zero]
    intro
    exact (Metric.closedBall_subset_ball hn).trans (Set.subset_iUnion_of_subset _ le_rfl)

end FiniteResidueField

section CompactDVR

open Valued

variable (K) in
lemma exists_norm_coe_lt_one : âˆƒ x : ğ’ª[K], 0 < â€–(x : K)â€– âˆ§ â€–(x : K)â€– < 1 := by
  obtain âŸ¨x, hx, hx'âŸ© := NormedField.exists_norm_lt_one K
  refine âŸ¨âŸ¨x, hx'.leâŸ©, ?_âŸ©
  simpa [hx', Subtype.ext_iff] using hx

variable (K) in
lemma exists_norm_lt_one : âˆƒ x : ğ’ª[K], 0 < â€–xâ€– âˆ§ â€–xâ€– < 1 :=
  exists_norm_coe_lt_one K

variable (K) in
lemma exists_nnnorm_lt_one : âˆƒ x : ğ’ª[K], 0 < â€–xâ€–â‚Š âˆ§ â€–xâ€–â‚Š < 1 :=
  exists_norm_coe_lt_one K

lemma isPrincipalIdealRing_of_compactSpace {F Î“â‚€} [Field F]
    [LinearOrderedCommGroupWithZero Î“â‚€] [MulArchimedean Î“â‚€] [hv : Valued F Î“â‚€] [CompactSpace ğ’ª[F]]
    (h : âˆƒ x : F, 0 < Valued.v x âˆ§ Valued.v x < 1) :
    IsPrincipalIdealRing ğ’ª[F] := by
  -- TODO: generalize to `Valuation.Integer`, which will require showing that `IsCompact`
  -- pulls back across `TopologicalSpace.induced` from a `LocallyCompactSpace`.
  -- The strategy to show that we have a PIR is by contradiction,
  -- assuming that the range of the valuation is densely ordered.
  -- We can construct a family of spheres at every single element of the valuation ring
  -- outside of a closed ball, which will cover.
  -- Since we are in a compact space, this cover has a finite subcover.
  -- This subcover, when excluding the sphere at 1, then has a max element since it is finite.
  -- However, since we are densely ordered, we can find an element with a valuation between
  -- the max element and 1, which is a contradiction, since it is outside the cover.

  -- First, we need to pick a threshold element with a nontrivial valuation less than 1,
  -- which will form -- the inner closed ball of the cover, which we need to cover 0.
  -- We have such an element by construction.
  obtain âŸ¨x, hx, hx'âŸ© : âˆƒ x : ğ’ª[F], 0 < hv.v x âˆ§ hv.v x < 1 := by
    obtain âŸ¨x, hx, hx'âŸ© := h
    refine âŸ¨âŸ¨x, hx'.leâŸ©, hx, hx'âŸ©
  -- the key result is that a valuation ring that maps into a `MulArchimedean` value group
  -- is a PIR iff the value group is not densely ordered.
  have hi : Valuation.Integers (R := F) Valued.v ğ’ª[F] := Valuation.integer.integers v
  rw [hi.isPrincipalIdealRing_iff_not_denselyOrdered]
  intro H
  -- Construct our cover, which has an inner closed ball, and spheres for each element
  -- outside of the closed ball. These are all open sets by the nonarchimedean property.
  let U : ğ’ª[F] â†’ Set ğ’ª[F] := fun y â†¦ if hv.v y < hv.v x
    then {z | hv.v z â‰¤ hv.v x}
    else {z | hv.v z = hv.v y}
  -- Extract out the finite subcover from our cover, which is a finite set of elements of
  -- the valuation ring, whose spheres cover the whole ring.
  obtain âŸ¨t, htâŸ© := CompactSpace.elim_nhds_subcover U <| by
    intro y
    simp only [U]
    split_ifs with hy
    Â· refine IsOpen.mem_nhds ((Valued.isOpen_closedball _ hx.ne').preimage ?_) ?_
      Â· exact continuous_subtype_val
      Â· simp [hy.le]
    Â· refine IsOpen.mem_nhds ((Valued.isOpen_sphere _ ?_).preimage ?_) ?_
      Â· simp only [not_lt] at hy
        exact (hx.trans_le hy).ne'
      Â· exact continuous_subtype_val
      Â· simp
  -- For each element of the valuation ring that is bigger than our threshold element above,
  -- then there must be something in the cover that has the precise valuation of the element,
  -- because it must be outside the inner closed ball, and thus is covered by some sphere.
  have htm : âˆ€ y : ğ’ª[F], hv.v x < hv.v y â†’ âˆƒ z âˆˆ t, hv.v z = hv.v y := by
    intro y hy
    have := ht.ge (Set.mem_univ y)
    simp only [Set.mem_iUnion, exists_prop', nonempty_prop, U] at this
    -- we get the `z` from the cover that covers our arbitrary `y` with its set
    obtain âŸ¨z, hz, hz'âŸ© := this
    -- and this `z` is either less than or greater than (or equal to) the threshold element
    split_ifs at hz' with h
    -- the `z` is inside closed ball case, which is a contradiction since we know `y` is outside
    Â· simp [hy.not_ge] at hz'
    -- the `z` is gives a sphere, so we plug it in
    Â· simp only [Set.mem_setOf_eq, U] at hz'
      exact âŸ¨z, hz, hz'.symmâŸ©
  -- Pick an element of the valuation ring to use as the excluded element of the subcover
  -- (since we know that all elements of the valuation ring have valuation less than or equal to 1).
  obtain âŸ¨y, _, hy'âŸ© : âˆƒ y : ğ’ª[F], y âˆˆ t âˆ§ hv.v y = 1 := by simpa using htm 1 (by simpa using hx')
  -- And pick an element in the subcover that is greater than the threshold element, but less
  -- than valuation 1. We will need this to show that the subcover excluding the element
  -- with valuation 1 is nonempty, which will allow us to take a max element.
  obtain âŸ¨w, hwt, hw1, hxwâŸ© : âˆƒ w : ğ’ª[F], w âˆˆ t âˆ§ hv.v w < 1 âˆ§ hv.v x < hv.v w := by
    replace hx' : (âŸ¨_, x, rflâŸ© : Set.range hv.v) < âŸ¨_, 1, rflâŸ© := by simpa using hx'
    obtain âŸ¨âŸ¨_, w, rflâŸ©, hw, hw'âŸ© := exists_between hx'
    obtain âŸ¨u, hu, hu'âŸ© := htm âŸ¨w, by simpa using hw'.leâŸ© hw
    exact âŸ¨u, hu, hu' â–¸ by simpa using hw', hu' â–¸ by simpa using hwâŸ©
  -- We're ready to work with the cover that excludes elements with valuation 1.
  let u := t.filter (fun a : ğ’ª[F] â†¦ hv.v a < 1)
  have hwu : w âˆˆ u := by simp [u, hwt, hw1] -- and it is nonempty.
  -- So the element that takes on the largest valuation in this partial cover is in the cover itself
  -- which is the case because this partial cover is closed under the max (`âŠ”`) operation:
  -- if `â€–xâ€– âˆˆ u` and `â€–yâ€– âˆˆ u`, then `max â€–xâ€– â€–yâ€– âˆˆ u`
  obtain hl' := (u.image (hv.v âˆ˜ ((â†‘) : ğ’ª[F] â†’ F))).max'_mem <| by simpa using âŸ¨_, hwuâŸ©
  simp only [Finset.mem_image, Finset.mem_filter, Function.comp_apply, u] at hl'
  obtain âŸ¨l, âŸ¨hl, hl'âŸ©, hvlâŸ© := hl'
  -- we know that this largest element must have valuation less than 1,
  -- since it is in the partial cover, and this is the property of the partial cover
  have hm : (âŸ¨hv.v l, l, rflâŸ© : Set.range hv.v) < (âŸ¨1, y, hy'âŸ©) := by simpa using hl'
  -- Prepare the contradiction, pick an element that has a valuation between the max element and 1.
  obtain âŸ¨âŸ¨_, m, rflâŸ©, hmâŸ© := exists_between hm
  simp only [Subtype.mk_lt_mk] at hm
  -- well, it is in the ring, so there is something in the cover that covers it,
  -- and it must be a sphere since it is larger than the threshold element by virtue of
  -- `v x < v l < v m`.
  obtain âŸ¨n, hn, hn'âŸ© : âˆƒ n âˆˆ t, hv.v n = hv.v m := by
    refine htm âŸ¨m, hm.right.leâŸ© (hxw.trans (hm.left.trans_le' ?_))
    rw [hvl]
    refine Finset.le_max' _ _ ?_
    simp only [Finset.mem_image, Finset.mem_filter, Function.comp_apply]
    exact âŸ¨w, âŸ¨hwt, hw1âŸ©, rflâŸ©
  rw [â† hn'] at hm -- clean up what valuations we refer to
  -- to supply the contradiction, we have `v l < v n`, now prove that also `v n â‰¤ v l`
  refine hm.left.not_ge ?_
  -- which is the case since `â€–lâ€– = u.max' ..` and the property of `Finset.max'`
  rw [hvl]
  refine Finset.le_max' _ _ ?_
  simp only [Finset.mem_image, Finset.mem_filter, Function.comp_apply]
  use n
  simp [hn, hm.right]

lemma isDiscreteValuationRing_of_compactSpace [h : CompactSpace ğ’ª[K]] :
    IsDiscreteValuationRing ğ’ª[K] := by
  -- To prove we have a DVR, we need to show it is a local ring and a PIR and not a field.
  -- First, note that it is a local ring, which the TC knows about.
  -- We prove that it is not a field inline later.
  have hl : IsLocalRing ğ’ª[K] := inferInstance
  obtain âŸ¨x, hx, hx'âŸ© := exists_nnnorm_lt_one K
  rw [â† nnnorm_one (G := K)] at hx'
  have key : IsPrincipalIdealRing ğ’ª[K] :=
    isPrincipalIdealRing_of_compactSpace (NormedField.exists_norm_lt_one K)
  exact {
    __ := hl
    __ := key
    not_a_field' := by
      -- here is the other place where the nontriviality of the norm comes in,
      -- since if we had `â€–xâ€– = 0 âˆ¨ â€–xâ€– = 1`, then the maximal ideal would be `âŠ¥`.
      simp only [ne_eq, Ideal.ext_iff, IsLocalRing.mem_maximalIdeal, mem_nonunits_iff,
        Ideal.mem_bot, not_forall, isUnit_iff_norm_eq_one]
      refine âŸ¨x, ?_âŸ©
      simp only [â† coe_lt_coe, coe_zero, coe_nnnorm, norm_pos_iff, ne_eq,
        ZeroMemClass.coe_eq_zero, nnnorm_one, coe_one] at hx hx'
      simpa [hx] using hx'.ne
  }

end CompactDVR

lemma compactSpace_iff_completeSpace_and_isDiscreteValuationRing_and_finite_residueField :
    CompactSpace ğ’ª[K] â†” CompleteSpace ğ’ª[K] âˆ§ IsDiscreteValuationRing ğ’ª[K] âˆ§ Finite ğ“€[K] := by
  refine âŸ¨fun h â†¦ ?_, fun âŸ¨_, _, hâŸ© â†¦ âŸ¨?_âŸ©âŸ©
  Â· have : IsDiscreteValuationRing ğ’ª[K] := isDiscreteValuationRing_of_compactSpace
    refine âŸ¨complete_of_compact, by assumption, ?_âŸ©
    rw [â† isCompact_univ_iff, isCompact_iff_totallyBounded_isComplete,
        totallyBounded_iff_finite_residueField] at h
    exact h.left
  Â· rw [â† totallyBounded_iff_finite_residueField] at h
    rw [isCompact_iff_totallyBounded_isComplete]
    exact âŸ¨h, completeSpace_iff_isComplete_univ.mp â€¹_â€ºâŸ©

lemma properSpace_iff_compactSpace_integer :
    ProperSpace K â†” CompactSpace ğ’ª[K] := by
  simp only [â† isCompact_univ_iff, Subtype.isCompact_iff, Set.image_univ, Subtype.range_coe_subtype,
             mem_iff, â† mem_closedBall_zero_iff, Set.setOf_mem_eq]
  constructor <;> intro h
  Â· exact isCompact_closedBall 0 1
  Â· suffices LocallyCompactSpace K from .of_nontriviallyNormedField_of_weaklyLocallyCompactSpace K
    exact IsCompact.locallyCompactSpace_of_mem_nhds_of_addGroup h <|
      Metric.closedBall_mem_nhds 0 zero_lt_one

lemma properSpace_iff_completeSpace_and_isDiscreteValuationRing_integer_and_finite_residueField :
    ProperSpace K â†” CompleteSpace K âˆ§ IsDiscreteValuationRing ğ’ª[K] âˆ§ Finite ğ“€[K] := by
  simp only [properSpace_iff_compactSpace_integer,
      compactSpace_iff_completeSpace_and_isDiscreteValuationRing_and_finite_residueField,
      completeSpace_iff_isComplete_univ (Î± := ğ’ª[K]), Subtype.isComplete_iff,
      NormedField.completeSpace_iff_isComplete_closedBall, Set.image_univ,
      Subtype.range_coe_subtype, mem_iff, â† mem_closedBall_zero_iff, Set.setOf_mem_eq]

end Valued.integer
