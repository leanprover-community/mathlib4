/-
Copyright (c) 2025 Salvatore Mercuri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Salvatore Mercuri
-/
import Mathlib.RingTheory.Valuation.ValuativeRel.Basic
import Mathlib.Topology.UniformSpace.Completion
import Mathlib.Topology.Algebra.Valued.ValuationTopology
import Mathlib.NumberTheory.NumberField.Basic

/-!
# Ring topologised by a valuation

For a given valuation `v : Valuation R Î“â‚€` on a ring `R` taking values in `Î“â‚€`, this file
defines the type synonym `WithVal v` of `R`. By assigning a `Valued (WithVal v) Î“â‚€` instance,
`WithVal v` represents the ring `R` equipped with the topology coming from `v`. The type
synonym `WithVal v` is in isomorphism to `R` as rings via `WithVal.equiv v`. This
isomorphism should be used to explicitly map terms of `WithVal v` to terms of `R`.

The `WithVal` type synonym is used to define the completion of `R` with respect to `v` in
`Valuation.Completion`. An example application of this is
`IsDedekindDomain.HeightOneSpectrum.adicCompletion`, which is the completion of the field of
fractions of a Dedekind domain with respect to a height-one prime ideal of the domain.

## Main definitions
- `WithVal` : type synonym for a ring equipped with the topology coming from a valuation.
- `WithVal.equiv` : the canonical ring equivalence between `WithValuation v` and `R`.
- `Valuation.Completion` : the uniform space completion of a field `K` according to the
  uniform structure defined by the specified valuation.
-/

noncomputable section

variable {R Î“â‚€ : Type*} [LinearOrderedCommGroupWithZero Î“â‚€]

/-- Type synonym for a ring equipped with the topology coming from a valuation. -/
@[nolint unusedArguments]
def WithVal [Ring R] : Valuation R Î“â‚€ â†’ Type _ := fun _ => R

namespace WithVal

section Instances

variable {P S : Type*} [LinearOrderedCommGroupWithZero Î“â‚€]

instance [Ring R] (v : Valuation R Î“â‚€) : Ring (WithVal v) := inferInstanceAs (Ring R)

instance [CommRing R] (v : Valuation R Î“â‚€) : CommRing (WithVal v) := inferInstanceAs (CommRing R)

instance [Field R] (v : Valuation R Î“â‚€) : Field (WithVal v) := inferInstanceAs (Field R)

instance [Ring R] (v : Valuation R Î“â‚€) : Inhabited (WithVal v) := âŸ¨0âŸ©

instance [CommSemiring S] [CommRing R] [Algebra S R] (v : Valuation R Î“â‚€) :
    Algebra S (WithVal v) := inferInstanceAs (Algebra S R)

instance [CommRing S] [CommRing R] [Algebra S R] [IsFractionRing S R] (v : Valuation R Î“â‚€) :
    IsFractionRing S (WithVal v) := inferInstanceAs (IsFractionRing S R)

instance [Ring R] [SMul S R] (v : Valuation R Î“â‚€) : SMul S (WithVal v) :=
  inferInstanceAs (SMul S R)

instance [Ring R] [SMul P S] [SMul S R] [SMul P R] [IsScalarTower P S R] (v : Valuation R Î“â‚€) :
    IsScalarTower P S (WithVal v) :=
  inferInstanceAs (IsScalarTower P S R)

variable [CommRing R] (v : Valuation R Î“â‚€)

instance {S : Type*} [Ring S] [Algebra R S] :
    Algebra (WithVal v) S := inferInstanceAs (Algebra R S)

instance {S : Type*} [Ring S] [Algebra R S] (w : Valuation S Î“â‚€) :
    Algebra R (WithVal w) := inferInstanceAs (Algebra R S)

instance {P S : Type*} [Ring S] [Semiring P] [Module P R] [Module P S]
    [Algebra R S] [IsScalarTower P R S] :
    IsScalarTower P (WithVal v) S := inferInstanceAs (IsScalarTower P R S)

instance [Ring R] {Î“â‚€ : Type*} [LinearOrderedCommGroupWithZero Î“â‚€]
    {v : Valuation R Î“â‚€} : Preorder (WithVal v) := v.toPreorder

end Instances

section Ring

variable [Ring R] (v : Valuation R Î“â‚€)

/-- Canonical ring equivalence between `WithVal v` and `R`. -/
def equiv : WithVal v â‰ƒ+* R := RingEquiv.refl _

/-- Canonical valuation on the `WithVal v` type synonym. -/
def valuation : Valuation (WithVal v) Î“â‚€ := v.comap (equiv v)

@[simp] lemma valuation_equiv_symm (x : R) : valuation v ((equiv v).symm x) = v x := rfl

/-- Canonical ring equivalence between `WithVal v` and `WithVal w`. -/
def equivWithVal {Î“'â‚€ : Type*} [LinearOrderedCommGroupWithZero Î“'â‚€]
    (v : Valuation R Î“â‚€) (w : Valuation R Î“'â‚€) :
    WithVal v â‰ƒ+* WithVal w :=
  (WithVal.equiv v).trans (WithVal.equiv w).symm

instance {R} [Ring R] (v : Valuation R Î“â‚€) : Valued (WithVal v) Î“â‚€ :=
  Valued.mk' (valuation v)

theorem apply_equiv (r : WithVal v) : v (equiv v r) = Valued.v r := rfl

@[simp] theorem apply_symm_equiv (r : R) : Valued.v ((equiv v).symm r) = v r := rfl

theorem le_def {v : Valuation R Î“â‚€} {a b : WithVal v} :
    a â‰¤ b â†” v (equiv v a) â‰¤ v (equiv v b) := .rfl

theorem lt_def {v : Valuation R Î“â‚€} {a b : WithVal v} :
    a < b â†” v (equiv v a) < v (equiv v b) := .rfl

@[simp]
theorem equiv_equivWithVal_apply {Î“'â‚€ : Type*} [LinearOrderedCommGroupWithZero Î“'â‚€]
    (v : Valuation R Î“â‚€) (w : Valuation R Î“'â‚€) (x : WithVal v) :
    equiv w (equivWithVal v w x) = equiv v x := rfl

@[simp]
theorem equiv_equivWithVal_symm_apply {Î“'â‚€ : Type*} [LinearOrderedCommGroupWithZero Î“'â‚€]
    (v : Valuation R Î“â‚€) (w : Valuation R Î“'â‚€) (x : WithVal w) :
    equiv v ((equivWithVal v w).symm x) = equiv w x := rfl

end Ring

section CommRing

variable [CommRing R] (v : Valuation R Î“â‚€)

instance : ValuativeRel (WithVal v) := .ofValuation (valuation v)
instance : (valuation v).Compatible := .ofValuation (valuation v)

end CommRing

end WithVal

/-! The completion of a field with respect to a valuation. -/

namespace Valuation

open WithVal

variable {R : Type*} [Ring R] (v : Valuation R Î“â‚€)

/-- The completion of a field with respect to a valuation. -/
abbrev Completion := UniformSpace.Completion (WithVal v)

instance : Coe R v.Completion :=
  inferInstanceAs <| Coe (WithVal v) (UniformSpace.Completion (WithVal v))

section Equivalence

/-! The uniform isomorphism between `WithVal v` and `WithVal w` when `v` and `w` are
equivalent. -/

variable {R Î“â‚€ Î“â‚€' : Type*} [LinearOrderedCommGroupWithZero Î“â‚€] [LinearOrderedCommGroupWithZero Î“â‚€']

/-- If two valuations `v` and `w` are equivalent then `WithVal v` is order-isomorphic
to `WithVal w`. -/
def IsEquiv.orderRingIso [Ring R] {v : Valuation R Î“â‚€} {w : Valuation R Î“â‚€'} (h : v.IsEquiv w) :
    WithVal v â‰ƒ+*o WithVal w where
  __ := equivWithVal v w
  map_le_map_iff' := by
    intro a b
    simp [h.symm (equiv v a), le_def]

theorem IsEquiv.isUniformInducing_equivWithVal [DivisionRing R] {v : Valuation R Î“â‚€}
    {w : Valuation R Î“â‚€'} (hv : Function.Surjective v) (hw : Function.Surjective w)
    (h : v.IsEquiv w) : IsUniformInducing (equivWithVal v w) := by
  refine (isUniformInducing_iff _).2 <| Filter.ext fun u â†¦ ?_
  simp only [Filter.mem_comap, (Valued.hasBasis_uniformity _ _).mem_iff, true_and]
  refine âŸ¨fun âŸ¨t, âŸ¨Î³, hÎ³âŸ©, htuâŸ© â†¦ ?_, fun âŸ¨Î³, hÎ³âŸ© â†¦ ?_âŸ©
  Â· obtain âŸ¨a, haâŸ© := hw Î³
    have : v (equiv w ((equiv v).symm a)) â‰  0 := by
      simpa [map_eq_zero (equiv v).symm] using
        fun haâ‚€ â†¦ absurd (map_zero w â–¸ haâ‚€ â–¸ ha).symm (Units.ne_zero Î³)
    refine âŸ¨.mk0 _ this, Set.Subset.trans (fun p hp â†¦ Set.mem_preimage.2 ?_) htuâŸ©
    apply hÎ³
    rw [â† ha]
    rw [Units.val_mk0, â† equiv_equivWithVal_symm_apply v] at hp
    change equivWithVal v w p.2 - equivWithVal v w p.1 < (equiv w).symm a
    change p.2 - p.1 < h.orderRingIso.symm ((equiv w).symm a) at hp
    simpa using h.orderRingIso.lt_symm_apply.1 hp
  Â· use Prod.map (equivWithVal v w) (equivWithVal v w) '' u
    have hinj : (Prod.map (equivWithVal v w) (equivWithVal v w)).Injective :=
      Prod.map_injective.2 âŸ¨(equivWithVal v w).injective, (equivWithVal v w).injectiveâŸ©
    refine âŸ¨?_, subset_of_eq <| hinj.preimage_image uâŸ©
    obtain âŸ¨a, haâŸ© := hv Î³
    have : w (equiv v ((equiv w).symm a)) â‰  0 := by
      simpa [map_eq_zero (equiv w).symm] using
        fun haâ‚€ â†¦ absurd (map_zero v â–¸ haâ‚€ â–¸ ha).symm (Units.ne_zero Î³)
    refine âŸ¨.mk0 _ this, ?_âŸ©
    refine Set.Subset.trans (fun p hp â†¦ ?_) ((Set.image_subset_image_iff hinj).2 hÎ³)
    refine âŸ¨((equivWithVal v w).symm p.1, (equivWithVal v w).symm p.2), ?_, by simpâŸ©
    rw [â† ha]
    rw [Units.val_mk0, â† equiv_equivWithVal_apply v w] at hp
    change p.2 - p.1 < h.orderRingIso ((equiv v).symm a) at hp
    change (equivWithVal v w).symm p.2 - (equivWithVal v w).symm p.1 < (equiv v).symm a
    simpa using h.orderRingIso.symm_apply_lt.2 hp

/-- If two valuations `v` and `w` are equivalent then `WithVal v` and `WithVal w` are
isomorphic as uniform spaces. -/
@[simps!]
def IsEquiv.uniformEquiv [DivisionRing R] {v : Valuation R Î“â‚€} {v' : Valuation R Î“â‚€'}
    (hv : Function.Surjective v) (hv' : Function.Surjective v') (h : v.IsEquiv v') :
    WithVal v â‰ƒáµ¤ WithVal v' :=
  Equiv.toUniformEquivOfIsUniformInducing (WithVal.equivWithVal v v')
    (h.isUniformInducing_equivWithVal hv hv')

end Equivalence

end Valuation

namespace NumberField.RingOfIntegers

variable {K : Type*} [Field K] [NumberField K] (v : Valuation K Î“â‚€)

instance : CoeHead (ð“ž (WithVal v)) (WithVal v) := inferInstanceAs (CoeHead (ð“ž K) K)

instance : IsDedekindDomain (ð“ž (WithVal v)) := inferInstanceAs (IsDedekindDomain (ð“ž K))

instance (R : Type*) [CommRing R] [Algebra R K] [IsIntegralClosure R â„¤ K] :
    IsIntegralClosure R â„¤ (WithVal v) := â€¹IsIntegralClosure R â„¤ Kâ€º

/-- The ring equivalence between `ð“ž (WithVal v)` and an integral closure of
`â„¤` in `K`. -/
@[simps!]
def withValEquiv (R : Type*) [CommRing R] [Algebra R K] [IsIntegralClosure R â„¤ K] :
    ð“ž (WithVal v) â‰ƒ+* R := NumberField.RingOfIntegers.equiv R

end NumberField.RingOfIntegers

open scoped NumberField in
/-- The ring of integers of `WithVal v`, when `v` is a valuation on `â„š`, is
equivalent to `â„¤`. -/
@[simps! apply]
def Rat.ringOfIntegersWithValEquiv (v : Valuation â„š Î“â‚€) : ð“ž (WithVal v) â‰ƒ+* â„¤ :=
  NumberField.RingOfIntegers.withValEquiv v â„¤
