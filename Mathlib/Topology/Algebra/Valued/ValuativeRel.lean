/-
Copyright (c) 2025 Yakov Pechersky. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yakov Pechersky
-/
import Mathlib.RingTheory.Valuation.ValuativeRel.Basic
import Mathlib.Topology.Algebra.Valued.ValuationTopology
import Mathlib.Topology.Algebra.WithZeroTopology

/-!

# Valuative Relations with a Valuative Topology are Valued

The `IsValuativeTopology` is described in various equivalent forms as filter bases,
with helper lemmas to construct it under the `ContinuousConstVAdd R R` assumption.
When present on a `ValuativeRel R`, this implies `Valued R (ValueGroupWithZero R)`,
which is provided as an instance.

-/

open ValuativeRel TopologicalSpace Filter Topology Set

namespace IsValuativeTopology

variable {R : Type*} [CommRing R] [ValuativeRel R]

local notation "v" => valuation R

theorem hasBasis_nhds_iff [TopologicalSpace R] :
    IsValuativeTopology R â†” âˆ€ x : R,
    (ğ“ x).HasBasis (fun _ => True)
      fun Î³ : (ValueGroupWithZero R)Ë£ => { z | v (z - x) < Î³ } := by
  simp_rw [hasBasis_iff, â† neg_add_eq_sub]
  constructor <;> intro h
  Â· simp [mem_nhds_iff]
  Â· constructor
    simp [h]

/-- A version mentioning subtraction. -/
lemma mem_nhds_iff' [TopologicalSpace R] [IsValuativeTopology R] {s : Set R} {x : R} :
    s âˆˆ ğ“ x â†”
    âˆƒ Î³ : (ValueGroupWithZero R)Ë£, { z | v (z - x) < Î³ } âŠ† s := by
  simp [(hasBasis_nhds_iff.mp _ _).mem_iff]

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.mem_nhds := mem_nhds_iff'

section

variable [TopologicalSpace R] [ContinuousConstVAdd R R]

/-- Assuming `ContinuousConstVAdd R R`, we only need to check the neighbourhood of `0` in order to
prove `IsValuativeTopology R`. -/
theorem iff_nhds_zero :
    IsValuativeTopology R â†”
      âˆ€ s : Set R, s âˆˆ ğ“ 0 â†” âˆƒ Î³ : (ValueGroupWithZero R)Ë£, { z | v z < Î³ } âŠ† s := by
  constructor <;> intro h
  Â· simp [IsValuativeTopology.mem_nhds_iff]
  Â· constructor
    intro s x
    simpa [â† vadd_mem_nhds_vadd_iff (t := s) (-x), â† image_vadd, â† image_subset_iff] using
      h ((x + Â·) â»Â¹' s)

/-- Assuming `ContinuousConstVAdd R R`, we only need to check the neighbourhood of `0` in order to
prove `IsValuativeTopology R`. -/
lemma iff_hasBasis_zero :
    IsValuativeTopology R â†” (ğ“ (0 : R)).HasBasis (fun _ â†¦ True)
      fun Î³ : (ValueGroupWithZero R)Ë£ â†¦ { x | (valuation R) x < Î³ } :=
  iff_nhds_zero.trans âŸ¨fun h â†¦ âŸ¨by simpa using hâŸ©, fun h â†¦ by simp [h.mem_iff]âŸ©

end

lemma of_subgroups_basis {R : Type*} [CommRing R] [ValuativeRel R] :
    letI := ((valuation R).subgroups_basis.topology)
    IsValuativeTopology R :=
  letI := ((valuation R).subgroups_basis.topology)
  iff_hasBasis_zero.mpr ((valuation R).subgroups_basis.hasBasis_nhds_zero)

/-- The correctness result. -/
lemma _root_.isValuativeTopology_iff_subgroups_basis_topology_eq [t : TopologicalSpace R] :
    IsValuativeTopology R â†” (valuation R).subgroups_basis.topology = t := by
  let := (valuation R).subgroups_basis
  refine âŸ¨fun _ â†¦ ext_nhds fun x â†¦ Filter.ext fun s â†¦ ?_, ?_âŸ©
  Â· rw [(this.hasBasis_nhds _).mem_iff, mem_nhds_iff']; simp_rw [true_and]; rfl
  Â· rintro rfl
    exact .of_subgroups_basis

section

/-! # Results assuming IsValuativeTopology -/

variable [TopologicalSpace R] [IsValuativeTopology R]

variable (R) in
theorem hasBasis_nhds_zero :
    (ğ“ (0 : R)).HasBasis (fun _ => True)
      fun Î³ : (ValueGroupWithZero R)Ë£ => { x | v x < Î³ } := by
  simpa using hasBasis_nhds_iff.mp inferInstance (0 : R)

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.hasBasis_nhds_zero := hasBasis_nhds_zero

lemma mem_nhds_zero_iff (s : Set R) : s âˆˆ ğ“ (0 : R) â†”
    âˆƒ Î³ : (ValueGroupWithZero R)Ë£, { x | v x < Î³ } âŠ† s := by
  simp [(hasBasis_nhds_zero _).mem_iff]

@[deprecated (since := "2025-08-04")]
alias _root_.ValuativeTopology.mem_nhds_iff := mem_nhds_zero_iff

/-- Helper `Valued` instance when `ValuativeTopology R` over a `UniformSpace R`,
for use in porting files from `Valued` to `ValuativeRel`. -/
instance (priority := low) {R : Type*} [CommRing R] [ValuativeRel R] [UniformSpace R]
    [IsUniformAddGroup R] [IsValuativeTopology R] :
    Valued R (ValueGroupWithZero R) where
  Â«vÂ» := valuation R
  is_topological_valuation := mem_nhds_zero_iff

variable (R) in
instance (priority := low) instContinuousConstVAdd : ContinuousConstVAdd R R :=
  âŸ¨fun x â†¦ continuous_iff_continuousAt.2 fun z â†¦
    ((hasBasis_nhds_iff.mp inferInstance z).tendsto_iff
      (hasBasis_nhds_iff.mp inferInstance (x + z))).2 fun Î³ _ â†¦
      âŸ¨Î³, trivial, fun y hy â†¦ by simpa using hyâŸ©âŸ©

variable (R) in
instance (priority := low) isTopologicalAddGroup : IsTopologicalAddGroup R := by
  have basis := hasBasis_nhds_zero R
  refine .of_comm_of_nhds_zero ?_ ?_ fun xâ‚€ â†¦ (map_eq_of_inverse (-xâ‚€ + Â·) ?_ ?_ ?_).symm
  Â· exact (basis.prod_self.tendsto_iff basis).2 fun Î³ _ â†¦
      âŸ¨Î³, trivial, fun âŸ¨_, _âŸ© hx â†¦ (v).map_add_lt hx.left hx.rightâŸ©
  Â· exact (basis.tendsto_iff basis).2 fun Î³ _ â†¦ âŸ¨Î³, trivial, fun y hy â†¦ by simpa using hyâŸ©
  Â· ext; simp
  Â· simpa [ContinuousAt] using (continuous_const_vadd xâ‚€).continuousAt (x := (0 : R))
  Â· simpa [ContinuousAt] using (continuous_const_vadd (-xâ‚€)).continuousAt (x := xâ‚€)

instance (priority := low) : IsTopologicalRing R :=
  letI := IsTopologicalAddGroup.toUniformSpace R
  letI := isUniformAddGroup_of_addCommGroup (G := R)
  inferInstance

theorem isOpen_ball (r : ValueGroupWithZero R) :
    IsOpen {x | v x < r} := by
  rw [isOpen_iff_mem_nhds]
  rcases eq_or_ne r 0 with rfl | hr
  Â· simp
  Â· intro x hx
    rw [mem_nhds_iff']
    simp only [setOf_subset_setOf]
    exact âŸ¨Units.mk0 _ hr,
      fun y hy => (sub_add_cancel y x).symm â–¸ ((v).map_add _ x).trans_lt (max_lt hy hx)âŸ©

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.isOpen_ball := isOpen_ball

theorem isClosed_ball (r : ValueGroupWithZero R) :
    IsClosed {x | v x < r} := by
  rcases eq_or_ne r 0 with rfl | hr
  Â· simp
  Â· exact AddSubgroup.isClosed_of_isOpen (Valuation.ltAddSubgroup v (Units.mk0 r hr))
      (isOpen_ball _)

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.isClosed_ball := isClosed_ball

theorem isClopen_ball (r : ValueGroupWithZero R) :
    IsClopen {x | v x < r} :=
  âŸ¨isClosed_ball _, isOpen_ball _âŸ©

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.isClopen_ball := isClopen_ball

lemma isOpen_closedBall {r : ValueGroupWithZero R} (hr : r â‰  0) :
    IsOpen {x | v x â‰¤ r} := by
  rw [isOpen_iff_mem_nhds]
  intro x hx
  rw [mem_nhds_iff']
  simp only [setOf_subset_setOf]
  exact âŸ¨Units.mk0 _ hr, fun y hy => (sub_add_cancel y x).symm â–¸
    le_trans ((v).map_add _ _) (max_le (le_of_lt hy) hx)âŸ©

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.isOpen_closedBall := isOpen_closedBall

theorem isClosed_closedBall (r : ValueGroupWithZero R) :
    IsClosed {x | v x â‰¤ r} := by
  rw [â† isOpen_compl_iff, isOpen_iff_mem_nhds]
  intro x hx
  simp only [mem_compl_iff, mem_setOf_eq, not_le] at hx
  rw [mem_nhds_iff']
  have hx' : v x â‰  0 := ne_of_gt <| lt_of_le_of_lt zero_le' <| hx
  exact âŸ¨Units.mk0 _ hx', fun y hy hy' => ne_of_lt hy <| Valuation.map_sub_swap v x y â–¸
      (Valuation.map_sub_eq_of_lt_left _ <| lt_of_le_of_lt hy' hx)âŸ©

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.isClosed_closedBall := isClosed_closedBall

theorem isClopen_closedBall {r : ValueGroupWithZero R} (hr : r â‰  0) :
    IsClopen {x | v x â‰¤ r} :=
  âŸ¨isClosed_closedBall _, isOpen_closedBall hrâŸ©

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.isClopen_closedBall := isClopen_closedBall

theorem isClopen_sphere {r : ValueGroupWithZero R} (hr : r â‰  0) :
    IsClopen {x | v x = r} := by
  have h : {x : R | v x = r} = {x | v x â‰¤ r} \ {x | v x < r} := by
    ext x
    simp [â† le_antisymm_iff]
  rw [h]
  exact IsClopen.diff (isClopen_closedBall hr) (isClopen_ball _)

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.isClopen_sphere := isClopen_sphere

lemma isOpen_sphere {r : ValueGroupWithZero R} (hr : r â‰  0) :
    IsOpen {x | v x = r} :=
  isClopen_sphere hr |>.isOpen

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.isOpen_sphere := isOpen_sphere

open WithZeroTopology in
lemma continuous_valuation : Continuous v := by
  simp only [continuous_iff_continuousAt, ContinuousAt]
  rintro x
  by_cases hx : v x = 0
  Â· simpa [hx, (hasBasis_nhds_iff.mp inferInstance _).tendsto_iff
      WithZeroTopology.hasBasis_nhds_zero,
      Valuation.map_sub_of_right_eq_zero _ hx] using fun i hi â†¦ âŸ¨.mk0 i hi, fun y â†¦ idâŸ©
  Â· simpa [(hasBasis_nhds_iff.mp inferInstance _).tendsto_iff
      (WithZeroTopology.hasBasis_nhds_of_ne_zero hx)]
      using âŸ¨.mk0 (v x) hx, fun _ â†¦ Valuation.map_eq_of_sub_lt _âŸ©
end

section

/-! # Alternate constructors -/

variable [TopologicalSpace R] [ContinuousConstVAdd R R]

/-- A "metatheorem" saying that if we proved that a valuative topology has a certain basis of
`nhds 0`, then any topology having the same basis of `nhds 0` which is also `ContinuousConstVAdd` is
automatically an `IsValuativeTopology`. -/
theorem of_hasBasis {R : Type*} [CommRing R] [ValuativeRel R]
    {Î¹ : Type*} (p : Î¹ â†’ Prop) (s : Î¹ â†’ Set R)
    (ih : âˆ€ [TopologicalSpace R] [IsValuativeTopology R], (nhds 0).HasBasis p s)
    [Ï„ : TopologicalSpace R] [ContinuousConstVAdd R R] (h : (nhds 0).HasBasis p s) :
    IsValuativeTopology R := by
  rw [isValuativeTopology_iff_subgroups_basis_topology_eq]
  let := (valuation R).subgroups_basis
  specialize @ih this.topology of_subgroups_basis
  refine ext_nhds fun x â†¦ Filter.ext fun t â†¦ ?_
  rw [â† @vadd_mem_nhds_vadd_iff _ _ this.topology _ _ _ _ (-x),
    â† @vadd_mem_nhds_vadd_iff _ _ Ï„ _ _ _ _ (-x),
    vadd_eq_add, neg_add_cancel, h.mem_iff, ih.mem_iff]

lemma iff_hasBasis_ne_zero :
    IsValuativeTopology R â†”
    (ğ“ (0 : R)).HasBasis (Â· â‰  0) fun Î³ â†¦ { x | (valuation R) x < Î³ } :=
  iff_hasBasis_zero.trans <| HasBasis.to_hasBasis_iff
    (fun Î³ _ â†¦ âŸ¨Î³, Î³.ne_zero, subset_rflâŸ©)
    (fun _ h â†¦ âŸ¨Units.mk0 _ h, trivial, subset_rflâŸ©)

variable (R) in
lemma hasBasis_nhds_zero_ne_zero [IsValuativeTopology R] :
    (ğ“ (0 : R)).HasBasis (Â· â‰  0) ({ x | (valuation R) x < Â· }) :=
  iff_hasBasis_ne_zero.mp inferInstance

lemma of_hasBasis_ne_zero
    (h : (ğ“ (0 : R)).HasBasis (Â· â‰  0) fun Î³ â†¦ { x | (valuation R) x < Î³ }) :
    IsValuativeTopology R :=
  iff_hasBasis_ne_zero.mpr h

lemma iff_hasBasis_ne_zero_and_le_one :
    IsValuativeTopology R â†”
    (ğ“ (0 : R)).HasBasis (fun Î³ â†¦ Î³ â‰  0 âˆ§ Î³ â‰¤ 1) fun Î³ â†¦ { x | (valuation R) x < Î³ } :=
  iff_hasBasis_ne_zero.trans <| HasBasis.to_hasBasis_iff
    (fun Î³ hÎ³ â†¦ âŸ¨min Î³ 1, âŸ¨zero_lt_iff.1 <| lt_min (zero_lt_iff.2 hÎ³) zero_lt_one,
      min_le_right _ _âŸ©, fun x hx â†¦ hx.trans_le (a := v x) (min_le_left _ _)âŸ©)
    (fun Î³ hÎ³ â†¦ âŸ¨Î³, hÎ³.1, subset_refl _âŸ©)

lemma hasBasis_nhds_zero_ne_zero_and_le_one [IsValuativeTopology R] :
    (ğ“ (0 : R)).HasBasis (fun Î³ â†¦ Î³ â‰  0 âˆ§ Î³ â‰¤ 1) ({ x | (valuation R) x < Â· }) :=
  iff_hasBasis_ne_zero_and_le_one.mp inferInstance

lemma of_hasBasis_ne_zero_and_le_one
    (h : (ğ“ (0 : R)).HasBasis (fun Î³ â†¦ Î³ â‰  0 âˆ§ Î³ â‰¤ 1) fun Î³ â†¦ { x | (valuation R) x < Î³ }) :
    IsValuativeTopology R :=
  iff_hasBasis_ne_zero_and_le_one.mpr h

lemma iff_hasBasis_pair :
    IsValuativeTopology R â†”
    (ğ“ (0 : R)).HasBasis (fun rs : R Ã— R â†¦ rs.1 âˆˆ posSubmonoid R âˆ§ rs.2 âˆˆ posSubmonoid R)
      fun rs â†¦ { x | x * rs.2 <áµ¥ rs.1 } := by
  rw [iff_hasBasis_zero]
  refine HasBasis.to_hasBasis_iff ?_ ?_
  Â· simp only [posSubmonoid_def, setOf_subset_setOf, Prod.exists, forall_const]
    intro Î³
    obtain âŸ¨r, s, hâŸ© := valuation_surjective Î³.val
    by_cases hr : valuation R r = 0
    Â· simp [hr, eq_comm] at h
    Â· refine âŸ¨r, s, âŸ¨by simpa [valuation_eq_zero_iff] using hr, s.propâŸ©, ?_âŸ©
      simp only [â† h]
      intro x hx
      rw [lt_div_iffâ‚€ (by simp [zero_lt_iff])]
      simp [valuation, hx]
  Â· rintro âŸ¨r, sâŸ© âŸ¨hr, hsâŸ©
    refine âŸ¨Units.mk0 (.mk r âŸ¨s, hsâŸ©) ?_, trivial, ?_âŸ©
    Â· simpa using hr
    Â· simp [valuation]

lemma hasBasis_nhds_zero_pair [IsValuativeTopology R] :
    (ğ“ (0 : R)).HasBasis (fun rs : R Ã— R â†¦ rs.1 âˆˆ posSubmonoid R âˆ§ rs.2 âˆˆ posSubmonoid R)
      fun rs â†¦ { x | x * rs.2 <áµ¥ rs.1 } :=
  iff_hasBasis_pair.mp inferInstance

lemma of_hasBasis_pair
    (h : (ğ“ (0 : R)).HasBasis (fun rs : R Ã— R â†¦ rs.1 âˆˆ posSubmonoid R âˆ§ rs.2 âˆˆ posSubmonoid R)
      fun rs â†¦ { x | x * rs.2 <áµ¥ rs.1 }) :
    IsValuativeTopology R :=
  iff_hasBasis_pair.mpr h

lemma iff_hasBasis_min_inv :
    IsValuativeTopology R â†”
    (ğ“ (0 : R)).HasBasis (fun r : R â†¦ r âˆˆ posSubmonoid R)
      fun r â†¦ { x | x <áµ¥ r âˆ§ x * r <áµ¥ 1 } := by
  rw [iff_hasBasis_pair]
  refine HasBasis.to_hasBasis_iff ?_ ?_
  Â· rintro âŸ¨x, yâŸ© âŸ¨hx, hyâŸ©
    obtain hxy | hxy := rel_total (x * y) 1
    Â· refine âŸ¨x * x, mul_mem hx hx, setOf_subset_setOf.mpr fun z hz â†¦ ?_âŸ©
      simp only [Valuation.Compatible.rel_iff_le (Â«vÂ» := v),
        Valuation.Compatible.rel_lt_iff_lt (Â«vÂ» := v), map_mul] at *
      refine ((mul_lt_mul_right (zero_lt_iff.2 ((v).apply_posSubmonoid_ne_zero
        âŸ¨y, hyâŸ©))).2 hz.1).trans_le ?_
      rw [mul_assoc]
      exact (mul_le_iff_le_one_right (zero_lt_iff.2 ((v).apply_posSubmonoid_ne_zero
        âŸ¨x, hxâŸ©))).2 hxy
    Â· refine âŸ¨y * y, mul_mem hy hy, setOf_subset_setOf.mpr fun z hz â†¦ ?_âŸ©
      simp only [Valuation.Compatible.rel_iff_le (Â«vÂ» := v),
        Valuation.Compatible.rel_lt_iff_lt (Â«vÂ» := v), map_mul] at *
      rw [â† mul_lt_mul_right (zero_lt_iff.2 ((valuation R).apply_posSubmonoid_ne_zero
         âŸ¨y, hyâŸ©)), mul_assoc]
      exact hz.2.trans_le hxy
  Â· rintro x hx
    obtain hx1 | h1x := le_total (v x) 1
    Â· refine âŸ¨(x, 1), âŸ¨hx, one_mem _âŸ©, setOf_subset_setOf.mpr fun z hz â†¦ âŸ¨?_, ?_âŸ©âŸ©
      Â· rwa [mul_one] at hz
      Â· simp only [Valuation.Compatible.rel_lt_iff_lt (Â«vÂ» := v), map_mul, map_one, mul_one] at hz âŠ¢
        rw [â† mul_lt_mul_right (zero_lt_iff.2 ((valuation R).apply_posSubmonoid_ne_zero
          âŸ¨x, hxâŸ©))] at hz
        exact hz.trans_le (mul_le_one' hx1 hx1)
    Â· refine âŸ¨(1, x), âŸ¨one_mem _, hxâŸ©, setOf_subset_setOf.mpr fun z hz â†¦ âŸ¨?_, hzâŸ©âŸ©
      simp only [Valuation.Compatible.rel_lt_iff_lt (Â«vÂ» := v), map_mul, map_one] at hz âŠ¢
      refine lt_of_lt_of_le (lt_of_le_of_lt ?_ hz) h1x
      exact le_mul_of_one_le_right' h1x

lemma hasBasis_nhds_zero_min_inv [IsValuativeTopology R] :
    (ğ“ (0 : R)).HasBasis (fun r : R â†¦ r âˆˆ posSubmonoid R)
      fun r â†¦ { x | x <áµ¥ r âˆ§ x * r <áµ¥ 1 } :=
  iff_hasBasis_min_inv.mp inferInstance

lemma of_hasBasis_min_inv
    (h : (ğ“ (0 : R)).HasBasis (fun r : R â†¦ r âˆˆ posSubmonoid R)
      fun r â†¦ { x | x <áµ¥ r âˆ§ x * r <áµ¥ 1 }) :
    IsValuativeTopology R :=
  iff_hasBasis_min_inv.mpr h

lemma iff_hasBasis_compatible {Î“â‚€ : Type*} [LinearOrderedCommMonoidWithZero Î“â‚€]
    {v' : Valuation R Î“â‚€} [v'.Compatible] :
    IsValuativeTopology R â†”
    (ğ“ (0 : R)).HasBasis (fun rs : R Ã— R â†¦ v' rs.1 â‰  0 âˆ§ v' rs.2 â‰  0)
      fun rs : R Ã— R â†¦ { x | v' x * v' rs.2 < v' rs.1 } := by
  rw [iff_hasBasis_pair]
  refine HasBasis.to_hasBasis_iff ?_ ?_ <;>
  Â· simp [Valuation.Compatible.rel_iff_le (Â«vÂ» := v'),
      Valuation.Compatible.rel_lt_iff_lt (Â«vÂ» := v')];
    grind

lemma hasBasis_nhds_zero_compatible {Î“â‚€ : Type*} [LinearOrderedCommMonoidWithZero Î“â‚€]
    {v' : Valuation R Î“â‚€} [v'.Compatible] [IsValuativeTopology R] :
    (ğ“ (0 : R)).HasBasis (fun rs : R Ã— R â†¦ v' rs.1 â‰  0 âˆ§ v' rs.2 â‰  0)
      fun rs : R Ã— R â†¦ { x | v' x * v' rs.2 < v' rs.1 } :=
  iff_hasBasis_compatible.mp inferInstance

lemma of_hasBasis_compatible {Î“â‚€ : Type*} [LinearOrderedCommMonoidWithZero Î“â‚€]
    {v' : Valuation R Î“â‚€} [v'.Compatible]
    (h : (ğ“ (0 : R)).HasBasis (fun rs : R Ã— R â†¦ v' rs.1 â‰  0 âˆ§ v' rs.2 â‰  0)
      fun rs : R Ã— R â†¦ { x | v' x * v' rs.2 < v' rs.1 }) :
    IsValuativeTopology R :=
  iff_hasBasis_compatible.mpr h

lemma iff_hasBasis_map_ne_zero
    {F : Type*} [Field F] [ValuativeRel F] [TopologicalSpace F] [ContinuousConstVAdd F F]
    {Î“â‚€ : Type*} [LinearOrderedCommGroupWithZero Î“â‚€]
    {v' : Valuation F Î“â‚€} [v'.Compatible] :
    IsValuativeTopology F â†”
    (ğ“ (0 : F)).HasBasis (fun r â†¦ v' r â‰  0) fun r â†¦ { x | v' x < v' r } :=
  (iff_hasBasis_compatible (v' := v')).trans <| HasBasis.to_hasBasis_iff
  (fun âŸ¨x, yâŸ© hxy â†¦ âŸ¨x / y, by simpa using hxy, by simp [lt_div_iffâ‚€ (zero_lt_iff.2 hxy.2)]âŸ©)
  fun x hx â†¦ âŸ¨(x, 1), by simp [hx], by simpâŸ©

lemma hasBasis_nhds_zero_map_ne_zero
    {F : Type*} [Field F] [ValuativeRel F] [TopologicalSpace F] [ContinuousConstVAdd F F]
    {Î“â‚€ : Type*} [LinearOrderedCommGroupWithZero Î“â‚€]
    {v' : Valuation F Î“â‚€} [v'.Compatible] [IsValuativeTopology F] :
    (ğ“ (0 : F)).HasBasis (fun r â†¦ v' r â‰  0) fun r â†¦ { x | v' x < v' r } :=
  iff_hasBasis_map_ne_zero.mp inferInstance

lemma of_hasBasis_map_ne_zero
    {F : Type*} [Field F] [ValuativeRel F] [TopologicalSpace F] [ContinuousConstVAdd F F]
    {Î“â‚€ : Type*} [LinearOrderedCommGroupWithZero Î“â‚€]
    {v' : Valuation F Î“â‚€} [v'.Compatible]
    (h : (ğ“ (0 : F)).HasBasis (fun r â†¦ v' r â‰  0) fun r â†¦ { x | v' x < v' r }) :
    IsValuativeTopology F :=
  iff_hasBasis_map_ne_zero.mpr h

end

end IsValuativeTopology

namespace ValuativeRel

@[inherit_doc]
scoped notation "ğ’ª[" R "]" => Valuation.integer (valuation R)

@[inherit_doc]
scoped notation "ğ“‚[" K "]" => IsLocalRing.maximalIdeal ğ’ª[K]

@[inherit_doc]
scoped notation "ğ“€[" K "]" => IsLocalRing.ResidueField ğ’ª[K]

end ValuativeRel
