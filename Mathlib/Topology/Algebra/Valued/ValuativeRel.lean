/-
Copyright (c) 2025 Yakov Pechersky. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yakov Pechersky
-/
import Mathlib.RingTheory.Valuation.ValuativeRel
import Mathlib.Topology.Algebra.Valued.ValuationTopology

/-!

# Valuative Relations as Valued

In this temporary file, we provide a helper instance
for `Valued R Œì` derived from a `ValuativeRel R`,
so that downstream files can refer to `ValuativeRel R`,
to facilitate a refactor.

-/

namespace ValuativeRel

variable {R : Type*} [CommRing R]

instance [UniformSpace R] [IsUniformAddGroup R] [ValuativeRel R] [IsValuativeTopology R] :
    Valued R (ValueGroupWithZero R) :=
  .mk (valuation R) IsValuativeTopology.mem_nhds_iff

end ValuativeRel

namespace IsValuativeTopology

variable {R : Type*} [CommRing R] [ValuativeRel R]

open ValuativeRel TopologicalSpace Filter Topology Set

lemma of_hasBasis [TopologicalSpace R] (h : (ùìù (0 : R)).HasBasis (fun _ ‚Ü¶ True)
    fun Œ≥ : (ValueGroupWithZero R)À£ ‚Ü¶ { x | (valuation R) x < Œ≥ }) :
    IsValuativeTopology R :=
  ‚ü®by simp [h.mem_iff]‚ü©

lemma of_hasBasis_pair [TopologicalSpace R]
    (h : (ùìù (0 : R)).HasBasis (fun rs : R √ó R ‚Ü¶ rs.1 ‚àà posSubmonoid R ‚àß rs.2 ‚àà posSubmonoid R)
      fun rs  ‚Ü¶ { x | x * rs.2 <·µ• rs.1 }) :
    IsValuativeTopology R := by
  refine of_hasBasis (h.to_hasBasis ?_ ?_)
  ¬∑ rintro ‚ü®r, s‚ü© ‚ü®hr, hs‚ü©
    refine ‚ü®Units.mk0 (.mk r ‚ü®s, hs‚ü©) ?_, trivial, ?_‚ü©
    ¬∑ simpa using hr
    ¬∑ simp [valuation]
  ¬∑ rintro Œ≥ -
    obtain ‚ü®r, s, h‚ü© := valuation_surjective Œ≥.val
    by_cases hr : valuation R r = 0
    ¬∑ simp [hr, eq_comm] at h
    ¬∑ refine ‚ü®‚ü®r, s‚ü©, ‚ü®by simpa [valuation_eq_zero_iff] using hr, s.prop‚ü©, ?_‚ü©
      simp only [‚Üê h, Set.setOf_subset_setOf]
      intro x hx
      rw [lt_div_iff‚ÇÄ (by simp [zero_lt_iff])]
      simp [valuation, hx]

lemma of_hasBasis_compatible {Œì‚ÇÄ : Type*} [LinearOrderedCommMonoidWithZero Œì‚ÇÄ] [TopologicalSpace R]
    {v : Valuation R Œì‚ÇÄ} [v.Compatible]
    (h : (ùìù (0 : R)).HasBasis (fun rs : R √ó R ‚Ü¶ v rs.1 ‚â† 0 ‚àß v rs.2 ‚â† 0)
    fun rs : R √ó R ‚Ü¶ { x | v x * v rs.2 < v rs.1 }) :
    IsValuativeTopology R := by
  have : v.IsEquiv (valuation R) := isEquiv _ _
  refine of_hasBasis_pair (h.to_hasBasis ?_ ?_) <;>
  ¬∑ simp only [this.ne_zero, ne_eq, valuation_eq_zero_iff, posSubmonoid_def, setOf_subset_setOf,
    and_imp, Prod.exists, Prod.forall]
    intro r s hr hs
    refine ‚ü®r, s, ‚ü®hr, hs‚ü©, fun x ‚Ü¶ ?_‚ü©
    rw [‚Üê map_mul v, ‚Üê Valuation.Compatible.rel_lt_iff_lt]
    grind

variable [TopologicalSpace R] [IsValuativeTopology R]

variable (R) in
theorem hasBasis_nhds_zero :
    (ùìù (0 : R)).HasBasis (fun _ => True)
      fun Œ≥ : (ValueGroupWithZero R)À£ => { x | valuation _ x < Œ≥ } := by
  simp [Filter.hasBasis_iff, mem_nhds_iff]

variable (R) in
lemma hasBasis_nhds_zero_pair :
    (ùìù (0 : R)).HasBasis (fun rs : R √ó R ‚Ü¶ rs.1 ‚àà posSubmonoid R ‚àß rs.2 ‚àà posSubmonoid R)
      fun rs  ‚Ü¶ { x | x * rs.2 <·µ• rs.1 } := by
  refine (hasBasis_nhds_zero R).to_hasBasis ?_ ?_
  ¬∑ simp only [posSubmonoid_def, setOf_subset_setOf, Prod.exists, forall_const]
    intro Œ≥
    obtain ‚ü®r, s, h‚ü© := valuation_surjective Œ≥.val
    by_cases hr : valuation R r = 0
    ¬∑ simp [hr, eq_comm] at h
    ¬∑ refine ‚ü®r, s, ‚ü®by simpa [valuation_eq_zero_iff] using hr, s.prop‚ü©, ?_‚ü©
      simp only [‚Üê h]
      intro x hx
      rw [lt_div_iff‚ÇÄ (by simp [zero_lt_iff])]
      simp [valuation, hx]
  ¬∑ rintro ‚ü®r, s‚ü© ‚ü®hr, hs‚ü©
    refine ‚ü®Units.mk0 (.mk r ‚ü®s, hs‚ü©) ?_, trivial, ?_‚ü©
    ¬∑ simpa using hr
    ¬∑ simp [valuation]

lemma hasBasis_nhds_zero_compatible {Œì‚ÇÄ : Type*} [LinearOrderedCommMonoidWithZero Œì‚ÇÄ]
    (v : Valuation R Œì‚ÇÄ) [v.Compatible] :
    (ùìù (0 : R)).HasBasis (fun rs : R √ó R ‚Ü¶ v rs.1 ‚â† 0 ‚àß v rs.2 ‚â† 0)
      fun rs : R √ó R ‚Ü¶ { x | v x * v rs.2 < v rs.1 } := by
  have : v.IsEquiv (valuation R) := isEquiv _ _
  refine ((hasBasis_nhds_zero_pair R).to_hasBasis ?_ ?_) <;>
  ¬∑ simp only [this.ne_zero, ne_eq, valuation_eq_zero_iff, posSubmonoid_def, setOf_subset_setOf,
    and_imp, Prod.exists, Prod.forall]
    intro r s hr hs
    refine ‚ü®r, s, ‚ü®hr, hs‚ü©, fun x ‚Ü¶ ?_‚ü©
    rw [‚Üê map_mul v, ‚Üê Valuation.Compatible.rel_lt_iff_lt]
    grind

local notation "v" => valuation R

variable [IsTopologicalAddGroup R]

theorem mem_nhds {s : Set R} {x : R} :
    s ‚àà ùìù x ‚Üî ‚àÉ Œ≥ : (ValueGroupWithZero R)À£, { y | v (y - x) < Œ≥ } ‚äÜ s := by
  simp only [‚Üê nhds_translation_add_neg x, ‚Üê sub_eq_add_neg, preimage_setOf_eq, true_and,
    ((hasBasis_nhds_zero R).comap fun y => y - x).mem_iff]

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.mem_nhds := mem_nhds

theorem isOpen_ball (r : ValueGroupWithZero R) :
    IsOpen {x | v x < r} := by
  rw [isOpen_iff_mem_nhds]
  rcases eq_or_ne r 0 with rfl | hr
  ¬∑ simp
  ¬∑ intro x hx
    rw [mem_nhds]
    simp only [setOf_subset_setOf]
    exact ‚ü®Units.mk0 _ hr,
      fun y hy => (sub_add_cancel y x).symm ‚ñ∏ ((v).map_add _ x).trans_lt (max_lt hy hx)‚ü©

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.isOpen_ball := isOpen_ball

theorem isClosed_ball (r : ValueGroupWithZero R) :
    IsClosed {x | v x < r} := by
  rcases eq_or_ne r 0 with rfl | hr
  ¬∑ simp
  ¬∑ exact AddSubgroup.isClosed_of_isOpen (Valuation.ltAddSubgroup v (Units.mk0 r hr))
      (isOpen_ball _)

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.isClosed_ball := isClosed_ball

theorem isClopen_ball (r : ValueGroupWithZero R) :
    IsClopen {x | v x < r} :=
  ‚ü®isClosed_ball _, isOpen_ball _‚ü©

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.isClopen_ball := isClopen_ball

lemma isOpen_closedBall {r : ValueGroupWithZero R} (hr : r ‚â† 0) :
    IsOpen {x | v x ‚â§ r} := by
  rw [isOpen_iff_mem_nhds]
  intro x hx
  rw [mem_nhds]
  simp only [setOf_subset_setOf]
  exact ‚ü®Units.mk0 _ hr, fun y hy => (sub_add_cancel y x).symm ‚ñ∏
    le_trans ((v).map_add _ _) (max_le (le_of_lt hy) hx)‚ü©

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.isOpen_closedBall := isOpen_closedBall

theorem isClosed_closedBall (r : ValueGroupWithZero R) :
    IsClosed {x | v x ‚â§ r} := by
  rw [‚Üê isOpen_compl_iff, isOpen_iff_mem_nhds]
  intro x hx
  simp only [mem_compl_iff, mem_setOf_eq, not_le] at hx
  rw [mem_nhds]
  have hx' : v x ‚â† 0 := ne_of_gt <| lt_of_le_of_lt zero_le' <| hx
  exact ‚ü®Units.mk0 _ hx', fun y hy hy' => ne_of_lt hy <| Valuation.map_sub_swap v x y ‚ñ∏
      (Valuation.map_sub_eq_of_lt_left _ <| lt_of_le_of_lt hy' hx)‚ü©

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.isClosed_closedBall := isClosed_closedBall

theorem isClopen_closedBall {r : ValueGroupWithZero R} (hr : r ‚â† 0) :
    IsClopen {x | v x ‚â§ r} :=
  ‚ü®isClosed_closedBall _, isOpen_closedBall hr‚ü©

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.isClopen_closedBall := isClopen_closedBall

theorem isClopen_sphere {r : ValueGroupWithZero R} (hr : r ‚â† 0) :
    IsClopen {x | v x = r} := by
  have h : {x : R | v x = r} = {x | v x ‚â§ r} \ {x | v x < r} := by
    ext x
    simp [‚Üê le_antisymm_iff]
  rw [h]
  exact IsClopen.diff (isClopen_closedBall hr) (isClopen_ball _)

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.isClopen_sphere := isClopen_sphere

lemma isOpen_sphere {r : ValueGroupWithZero R} (hr : r ‚â† 0) :
    IsOpen {x | v x = r} :=
  isClopen_sphere hr |>.isOpen

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.isOpen_sphere := isOpen_sphere

end IsValuativeTopology

namespace Valued

variable {R : Type*} [CommRing R] [ValuativeRel R] [UniformSpace R]
  [IsUniformAddGroup R] [IsValuativeTopology R]

/-- Helper `Valued` instance when `ValuativeTopology R` over a `UniformSpace R`,
for use in porting files from `Valued` to `ValuativeRel`. -/
scoped instance : Valued R (ValuativeRel.ValueGroupWithZero R) where
  v := ValuativeRel.valuation R
  is_topological_valuation := IsValuativeTopology.mem_nhds_iff

end Valued

namespace ValuativeRel

@[inherit_doc]
scoped notation "ùí™[" R "]" => Valuation.integer (valuation R)

@[inherit_doc]
scoped notation "ùìÇ[" K "]" => IsLocalRing.maximalIdeal ùí™[K]

@[inherit_doc]
scoped notation "ùìÄ[" K "]" => IsLocalRing.ResidueField ùí™[K]

end ValuativeRel
