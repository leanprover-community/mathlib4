/-
Copyright (c) 2025 Yakov Pechersky. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yakov Pechersky
-/
import Mathlib.RingTheory.Valuation.ValuativeRel
import Mathlib.Topology.Algebra.Valued.ValuationTopology

/-!

# Valuative Relations as Valued

In this temporary file, we provide a helper instance
for `Valued R Œì` derived from a `ValuativeRel R`,
so that downstream files can refer to `ValuativeRel R`,
to facilitate a refactor.

-/

open ValuativeRel TopologicalSpace Filter Topology Set

/-- A density lemma saying that a fraction `x / y` can be bounded below by either `x * x` (written
without inverses) or `y‚Åª¬π * y‚Åª¬π` (written only with inverses).

In other words, for a linearly ordered monoid with zero `M` with a "groupification" `G`, `M ‚à™ M‚Åª¬π`
is dense in `G`.

See `ValuativeRel.exists_min_le` for an application. -/
lemma min_sq_sq_inv_le_div {Œì‚ÇÄ : Type*} [LinearOrderedCommGroupWithZero Œì‚ÇÄ]
    (x : Œì‚ÇÄ) {y : Œì‚ÇÄ} (hy : y ‚â† 0) :
    min (x * x) (y‚Åª¬π * y‚Åª¬π) ‚â§ x / y := by
  obtain hxy | hxy := le_total (x * y) 1
  ¬∑ calc
    _ ‚â§ x * x := min_le_left _ _
    _ = x / y * (x * y) := by rw [div_mul_eq_mul_div, mul_div_assoc, mul_div_cancel_right‚ÇÄ _ hy]
    _ ‚â§ x / y := mul_le_of_le_one_right' hxy
  ¬∑ calc
    _ ‚â§ y‚Åª¬π * y‚Åª¬π := min_le_right _ _
    _ ‚â§ y‚Åª¬π * y‚Åª¬π * (x * y) := le_mul_of_one_le_right' hxy
    _ = (x / y) := by rw [mul_comm, mul_assoc, ‚Üê mul_assoc y, mul_inv_cancel‚ÇÄ hy, one_mul,
        div_eq_mul_inv]

/-- The set `{ min (v r) (v r)‚Åª¬π | r ‚àà posSubmonoid R }` is dense in `Œì‚ÇÄ`.

Intuitively, `{v r} ‚à™ {(v r)‚Åª¬π}` "tends to" 0. -/
lemma ValuativeRel.exists_min_le {R : Type*} [CommRing R] [ValuativeRel R]
    (Œ≥ : (ValueGroupWithZero R)À£) :
    ‚àÉ r, valuation R r ‚â† 0 ‚àß min (valuation R r) (valuation R r)‚Åª¬π ‚â§ Œ≥ := by
  obtain ‚ü®x, y, hxy‚ü© := valuation_surjective Œ≥.val
  by_cases hx : valuation R x = 0
  ¬∑ exact (Œ≥.ne_zero <| by rw [‚Üê hxy, hx, zero_div, eq_comm]).elim
  have hy : valuation R y ‚â† 0 := valuation_eq_zero_iff.not.mpr y.prop
  obtain hxx | hyy := min_le_iff.1 <| min_sq_sq_inv_le_div (valuation R x) hy
  ¬∑ exact ‚ü®x * x, by simp [hx], min_le_of_left_le <| by simpa [‚Üê hxy] using hxx‚ü©
  ¬∑ exact ‚ü®y * y, by simp [hy], min_le_of_right_le <| by simpa [‚Üê hxy] using hyy‚ü©

namespace IsValuativeTopology

variable {R : Type*} [CommRing R] [ValuativeRel R]
  {F : Type*} [Field F] [ValuativeRel F]
  {Œì‚ÇÄ : Type*} [LinearOrderedCommMonoidWithZero Œì‚ÇÄ]
  {Œì‚ÇÄ' : Type*} [LinearOrderedCommGroupWithZero Œì‚ÇÄ']

local notation "v" => valuation R
local notation "v‚ÇÄ" => valuation F

/-- A version mentioning subtraction. -/
lemma mem_nhds_iff' [TopologicalSpace R] [IsValuativeTopology R] {s : Set R} {x : R} :
    s ‚àà ùìù (x : R) ‚Üî
    ‚àÉ Œ≥ : (ValueGroupWithZero R)À£, { z | v (z - x) < Œ≥ } ‚äÜ s := by
  convert mem_nhds_iff (s := s) using 4
  ext z
  simp [neg_add_eq_sub]

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.mem_nhds := mem_nhds_iff'

section

variable [TopologicalSpace R] [ContinuousConstVAdd R R]

/-- Assuming `ContinuousConstVAdd R R`, we only need to check the neighbourhood of `0` in order to
prove `IsValuativeTopology R`. -/
theorem of_zero
    (h‚ÇÄ : ‚àÄ s : Set R, s ‚àà ùìù 0 ‚Üî ‚àÉ Œ≥ : (ValueGroupWithZero R)À£, { z | v z < Œ≥ } ‚äÜ s) :
    IsValuativeTopology R where
  mem_nhds_iff {s x} := by
    simpa [‚Üê vadd_mem_nhds_vadd_iff (t := s) (-x), ‚Üê image_vadd, ‚Üê image_subset_iff] using
      h‚ÇÄ ((x + ¬∑) ‚Åª¬π' s)

/-- Assuming `ContinuousConstVAdd R R`, we only need to check the neighbourhood of `0` in order to
prove `IsValuativeTopology R`. -/
lemma of_hasBasis_zero (h : (ùìù (0 : R)).HasBasis (fun _ ‚Ü¶ True)
    fun Œ≥ : (ValueGroupWithZero R)À£ ‚Ü¶ { x | (valuation R) x < Œ≥ }) :
    IsValuativeTopology R :=
  .of_zero <| by simp [h.mem_iff]

end

instance of_subgroups_basis :
    letI := (v).subgroups_basis.topology
    IsValuativeTopology R :=
  letI := (v).subgroups_basis.topology
  of_hasBasis_zero <| (v).subgroups_basis.hasBasis_nhds_zero

/-- The correctness result. -/
lemma _root_.isValuativeTopology_iff_subgroups_basis_topology_eq [t : TopologicalSpace R] :
    IsValuativeTopology R ‚Üî (v).subgroups_basis.topology = t := by
  let := (valuation R).subgroups_basis
  refine ‚ü®fun _ ‚Ü¶ ext_nhds fun x ‚Ü¶ Filter.ext fun s ‚Ü¶ ?_, ?_‚ü©
  ¬∑ rw [(this.hasBasis_nhds _).mem_iff, mem_nhds_iff']; simp_rw [true_and]; rfl
  ¬∑ rintro rfl; infer_instance

example : ‚àÉ! _ : TopologicalSpace R, IsValuativeTopology R :=
  ‚ü®(v).subgroups_basis.topology, of_subgroups_basis, fun _ ht ‚Ü¶
    (isValuativeTopology_iff_subgroups_basis_topology_eq.mp ht).symm‚ü©

section

/-! # Results assuming IsValuativeTopology -/

variable [TopologicalSpace R] [IsValuativeTopology R]
  [TopologicalSpace F] [IsValuativeTopology F]

lemma mem_nhds_zero_iff (s : Set R) : s ‚àà ùìù (0 : R) ‚Üî
    ‚àÉ Œ≥ : (ValueGroupWithZero R)À£, { x | v x < Œ≥ } ‚äÜ s := by
  convert IsValuativeTopology.mem_nhds_iff' (x := (0 : R))
  rw [sub_zero]

@[deprecated (since := "2025-08-04")]
alias _root_.ValuativeTopology.mem_nhds_iff := mem_nhds_zero_iff

@[simp] lemma ball_mem_nhds_zero_iff (Œ≥ : ValueGroupWithZero R) :
    { x | v x < Œ≥ } ‚àà ùìù (0 : R) ‚Üî Œ≥ ‚â† 0 :=
  ‚ü®fun h hŒ≥0 ‚Ü¶ by simpa [hŒ≥0] using mem_of_mem_nhds h,
  fun hŒ≥0 ‚Ü¶ (mem_nhds_zero_iff _).mpr ‚ü®Units.mk0 _ hŒ≥0, subset_refl _‚ü©‚ü©

@[simp] lemma rel_ball_mem_nhds_zero_iff (r : R) :
    { x | x <·µ• r } ‚àà ùìù (0 : R) ‚Üî r ‚àà posSubmonoid R := by
  simp_rw [Valuation.Compatible.rel_lt_iff_lt (¬´v¬ª := v), ball_mem_nhds_zero_iff,
    ne_eq, valuation_eq_zero_iff, posSubmonoid_def]

@[simp] lemma rel_mul_ball_mem_nhds_zero_iff (r s : R) :
    { x | x * r <·µ• s } ‚àà ùìù (0 : R) ‚Üî s ‚àà posSubmonoid R := by
  simp_rw [Valuation.Compatible.rel_lt_iff_lt (¬´v¬ª := v), map_mul]
  by_cases hr : v r = 0
  ¬∑ simp_rw [hr, mul_zero]
    by_cases hs : v s = 0
    ¬∑ simp [hs, ‚Üê valuation_eq_zero_iff]
    ¬∑ simp [zero_lt_iff, hs, ‚Üê valuation_eq_zero_iff]
  simp_rw [‚Üê lt_div_iff‚ÇÄ (zero_lt_iff.2 hr), ball_mem_nhds_zero_iff, div_ne_zero_iff]
  simp [valuation_eq_zero_iff, hr]

/-- Helper `Valued` instance when `ValuativeTopology R` over a `UniformSpace R`,
for use in porting files from `Valued` to `ValuativeRel`. -/
instance (priority := low) {R : Type*} [CommRing R] [ValuativeRel R] [UniformSpace R]
    [IsUniformAddGroup R] [IsValuativeTopology R] :
    Valued R (ValueGroupWithZero R) where
  ¬´v¬ª := valuation R
  is_topological_valuation := mem_nhds_zero_iff

theorem hasBasis_nhds (x : R) :
    (ùìù x).HasBasis (fun _ => True)
      fun Œ≥ : (ValueGroupWithZero R)À£ => { z | v (z - x) < Œ≥ } := by
  simp [Filter.hasBasis_iff, mem_nhds_iff']

variable (R) in
theorem hasBasis_nhds_zero :
    (ùìù (0 : R)).HasBasis (fun _ => True)
      fun Œ≥ : (ValueGroupWithZero R)À£ => { x | v x < Œ≥ } := by
  convert hasBasis_nhds (0 : R); rw [sub_zero]

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.hasBasis_nhds_zero := hasBasis_nhds_zero

variable (R) in
lemma hasBasis_nhds_zero_ne_zero :
    (ùìù (0 : R)).HasBasis (¬∑ ‚â† 0) fun Œ≥ ‚Ü¶ { x | (valuation R) x < Œ≥ } :=
  (hasBasis_nhds_zero R).to_hasBasis'
    (fun Œ≥ _ ‚Ü¶ ‚ü®Œ≥, Œ≥.ne_zero, subset_refl _‚ü©)
    fun Œ≥ hŒ≥ ‚Ü¶ by simpa

variable (R) in
lemma hasBasis_nhds_zero_ne_zero_and_le_one :
    (ùìù (0 : R)).HasBasis (fun Œ≥ ‚Ü¶ Œ≥ ‚â† 0 ‚àß Œ≥ ‚â§ 1) fun Œ≥ ‚Ü¶ { x | (valuation R) x < Œ≥ } :=
  (hasBasis_nhds_zero_ne_zero R).to_hasBasis'
    (fun Œ≥ hŒ≥ ‚Ü¶ ‚ü®min Œ≥ 1, ‚ü®zero_lt_iff.1 <| lt_min (zero_lt_iff.2 hŒ≥) zero_lt_one,
      min_le_right _ _‚ü©, fun x hx ‚Ü¶ hx.trans_le (a := v x) (min_le_left _ _)‚ü©)
    fun Œ≥ hŒ≥ ‚Ü¶ by simp [hŒ≥.1]

variable (R) in
private lemma hasBasis_nhds_zero_pair_aux :
    (ùìù (0 : R)).HasBasis (fun rs : R √ó R ‚Ü¶ v rs.1 ‚â† 0 ‚àß v rs.2 ‚â† 0)
      fun rs ‚Ü¶ { x | v x < v rs.1 / v rs.2 } := by
  refine (hasBasis_nhds_zero_ne_zero R).to_hasBasis' (fun Œ≥ hŒ≥ ‚Ü¶ ?_) (by simp)
  obtain ‚ü®r, s, rfl‚ü© := valuation_surjective Œ≥
  by_cases hr : v r = 0
  ¬∑ exact (hŒ≥ <| by simp [hr]).elim
  ¬∑ exact ‚ü®(r, s), ‚ü®hr, valuation_eq_zero_iff.not.mpr s.prop‚ü©, subset_refl _‚ü©

variable (R) in
lemma hasBasis_nhds_zero_pair :
    (ùìù (0 : R)).HasBasis (fun rs : R √ó R ‚Ü¶ rs.1 ‚àà posSubmonoid R ‚àß rs.2 ‚àà posSubmonoid R)
      fun rs ‚Ü¶ { x | x * rs.2 <·µ• rs.1 } := by
  simp_rw [posSubmonoid_def, ‚Üê valuation_eq_zero_iff]
  exact (hasBasis_nhds_zero_pair_aux R).to_hasBasis'
    (fun p hp ‚Ü¶ ‚ü®p, hp, by simp_rw [lt_div_iff‚ÇÄ (zero_lt_iff.2 hp.2), ‚Üê map_mul,
      ‚Üê Valuation.Compatible.rel_lt_iff_lt, subset_refl]‚ü©)
    fun p hp ‚Ü¶ by simpa [valuation_eq_zero_iff] using hp.1

lemma hasBasis_nhds_zero_compatible (v' : Valuation R Œì‚ÇÄ) [v'.Compatible] :
    (ùìù (0 : R)).HasBasis (fun rs : R √ó R ‚Ü¶ v' rs.1 ‚â† 0 ‚àß v' rs.2 ‚â† 0)
      fun rs : R √ó R ‚Ü¶ { x | v' x * v' rs.2 < v' rs.1 } := by
  convert hasBasis_nhds_zero_pair R <;>
  simp [Valuation.Compatible.rel_iff_le (¬´v¬ª := v'), Valuation.Compatible.rel_lt_iff_lt (¬´v¬ª := v')]

variable (R) in
private lemma hasBasis_nhds_zero_min_inv_aux :
    (ùìù (0 : R)).HasBasis (fun r : R ‚Ü¶ v r ‚â† 0)
      fun r ‚Ü¶ { x | v x < min (v r) (v r)‚Åª¬π } :=
  (hasBasis_nhds_zero R).to_hasBasis'
    (fun Œ≥ _ ‚Ü¶ let ‚ü®r, hr0, hr‚ü© := exists_min_le Œ≥
      ‚ü®r, hr0, setOf_subset_setOf.mpr fun x hx ‚Ü¶ hx.trans_le hr‚ü©)
    fun r hr ‚Ü¶ by simp_rw [lt_min_iff, setOf_and]; exact inter_mem (by simpa) (by simpa)

lemma hasBasis_nhds_zero_compatible_min_inv (v' : Valuation R Œì‚ÇÄ') [v'.Compatible] :
    (ùìù (0 : R)).HasBasis (fun r : R ‚Ü¶ v' r ‚â† 0)
      fun r ‚Ü¶ { x | v' x < min (v' r) (v' r)‚Åª¬π } := by
  have e := isEquiv (v) v'
  convert hasBasis_nhds_zero_min_inv_aux R with _ _ x
  ¬∑ rw [‚Üê map_zero v', ‚Üê map_zero (v), e.val_eq]
  ¬∑ by_cases h : v x = 0
    ¬∑ have h' : v' x = 0 := by rwa [‚Üê map_zero v', ‚Üê e.val_eq, map_zero]
      simp [h, h']
    ¬∑ have h' : v' x ‚â† 0 := by rwa [‚Üê map_zero v', ne_eq, ‚Üê e.val_eq, map_zero]
      simp_rw [lt_min_iff, inv_eq_one_div, lt_div_iff‚ÇÄ (zero_lt_iff.2 h),
        lt_div_iff‚ÇÄ (zero_lt_iff.2 h'), ‚Üê map_mul, ‚Üê map_one (v), ‚Üê map_one (v'), e.lt_iff_lt]

variable (R) in
lemma hasBasis_nhds_zero_lt_and_mul_lt_one :
    (ùìù (0 : R)).HasBasis (fun r : R ‚Ü¶ r ‚àà posSubmonoid R)
      fun r ‚Ü¶ { x | x <·µ• r ‚àß x * r <·µ• 1 } := by
  refine (hasBasis_nhds_zero_min_inv_aux R).to_hasBasis' ?_ ?_
  ¬∑ intro r hr
    refine ‚ü®r, valuation_eq_zero_iff.not.mp hr, setOf_subset_setOf.mpr fun x hx ‚Ü¶ ?_‚ü©
    rwa [lt_min_iff, inv_eq_one_div, lt_div_iff‚ÇÄ (zero_lt_iff.2 hr), ‚Üê map_mul, ‚Üê (v).map_one,
      ‚Üê Valuation.Compatible.rel_lt_iff_lt, ‚Üê Valuation.Compatible.rel_lt_iff_lt]
  ¬∑ intro r hr
    rw [setOf_and]
    exact inter_mem (by simpa) (by simp)

variable (F) in
private lemma hasBasis_nhds_zero_ne_zero_aux :
    (ùìù (0 : F)).HasBasis (¬∑ ‚â† 0) fun r ‚Ü¶ { x | v‚ÇÄ x < v‚ÇÄ r } :=
  (hasBasis_nhds_zero_min_inv_aux F).to_hasBasis'
    (fun Œ≥ hŒ≥ ‚Ü¶ (le_total (v‚ÇÄ Œ≥) (v‚ÇÄ Œ≥)‚Åª¬π).casesOn
      (fun h ‚Ü¶ ‚ü®Œ≥, (map_ne_zero _).mp hŒ≥, by rw [min_eq_left h]‚ü©)
      fun h ‚Ü¶ ‚ü®Œ≥‚Åª¬π, by simpa using hŒ≥, by rw [min_eq_right h, (v‚ÇÄ).map_inv]‚ü©)
    fun Œ≥ hŒ≥ ‚Ü¶ by simpa

lemma hasBasis_nhds_zero_compatible_ne_zero (v' : Valuation F Œì‚ÇÄ') [v'.Compatible] :
    (ùìù (0 : F)).HasBasis (¬∑ ‚â† 0) fun r ‚Ü¶ { x | v' x < v' r } := by
  convert hasBasis_nhds_zero_ne_zero_aux F
  rw [(isEquiv v' (v‚ÇÄ)).lt_iff_lt]

variable (F) in
lemma hasBasis_nhds_zero_ne_zero' :
    (ùìù (0 : F)).HasBasis (¬∑ ‚â† 0) fun r ‚Ü¶ { x | x <·µ• r } :=
  (hasBasis_nhds_zero_ne_zero_aux F).to_hasBasis'
    (fun r hr ‚Ü¶ ‚ü®r, hr, by simp_rw [‚Üê Valuation.Compatible.rel_lt_iff_lt]; rfl‚ü©)
    fun r hr ‚Ü¶ by simpa [‚Üê valuation_eq_zero_iff]

variable (F) in
private lemma hasBasis_nhds_zero_ne_zero_and_le_one_aux :
    (ùìù (0 : F)).HasBasis (fun r ‚Ü¶ r ‚â† 0 ‚àß v‚ÇÄ r ‚â§ 1) fun r ‚Ü¶ { x | v‚ÇÄ x < v‚ÇÄ r } :=
  (hasBasis_nhds_zero_ne_zero_aux F).to_hasBasis'
    (fun r hr ‚Ü¶ (le_total (v‚ÇÄ r) 1).casesOn
      (fun h ‚Ü¶ ‚ü®r, ‚ü®hr, h‚ü©, subset_refl _‚ü©)
      fun h ‚Ü¶ ‚ü®r‚Åª¬π, ‚ü®inv_ne_zero hr, by rw [map_inv‚ÇÄ]; exact inv_le_one_of_one_le‚ÇÄ h‚ü©,
        setOf_subset_setOf.mpr fun x hx ‚Ü¶ hx.trans_le <| by
          rw [map_inv‚ÇÄ, inv_le_iff_one_le_mul‚ÇÄ (by simpa [zero_lt_iff])]
          exact one_le_mul h h‚ü©)
    fun r hr ‚Ü¶ by simp [hr.1]

lemma hasBasis_nhds_zero_compatible_ne_zero_and_le_one (v' : Valuation F Œì‚ÇÄ') [v'.Compatible] :
    (ùìù (0 : F)).HasBasis (fun r ‚Ü¶ r ‚â† 0 ‚àß v' r ‚â§ 1) fun r ‚Ü¶ { x | v' x < v' r } := by
  let e := isEquiv (v‚ÇÄ) v'
  convert hasBasis_nhds_zero_ne_zero_and_le_one_aux F
  ¬∑ rw [e.le_one_iff_le_one]
  ¬∑ rw [e.lt_iff_lt]

variable (F) in
lemma hasBasis_nhds_zero_ne_zero_and_le_one' :
    (ùìù (0 : F)).HasBasis (fun r ‚Ü¶ r ‚â† 0 ‚àß r ‚â§·µ• 1) fun r ‚Ü¶ { x | x <·µ• r } := by
  convert hasBasis_nhds_zero_ne_zero_and_le_one_aux F
  ¬∑ rw [‚Üê map_one v‚ÇÄ, ‚Üê Valuation.Compatible.rel_iff_le]
  ¬∑ rw [‚Üê Valuation.Compatible.rel_lt_iff_lt]

variable (R) in
instance (priority := low) isTopologicalAddGroup : IsTopologicalAddGroup R := by
  have cts_add : ContinuousConstVAdd R R :=
    ‚ü®fun x ‚Ü¶ continuous_iff_continuousAt.2 fun z ‚Ü¶
      ((hasBasis_nhds z).tendsto_iff (hasBasis_nhds (x + z))).2 fun Œ≥ _ ‚Ü¶
        ‚ü®Œ≥, trivial, fun y hy ‚Ü¶ by simpa using hy‚ü©‚ü©
  have basis := hasBasis_nhds_zero R
  refine .of_comm_of_nhds_zero ?_ ?_ fun x‚ÇÄ ‚Ü¶ (map_eq_of_inverse (-x‚ÇÄ + ¬∑) ?_ ?_ ?_).symm
  ¬∑ exact (basis.prod_self.tendsto_iff basis).2 fun Œ≥ _ ‚Ü¶
      ‚ü®Œ≥, trivial, fun ‚ü®_, _‚ü© hx ‚Ü¶ (v).map_add_lt hx.left hx.right‚ü©
  ¬∑ exact (basis.tendsto_iff basis).2 fun Œ≥ _ ‚Ü¶ ‚ü®Œ≥, trivial, fun y hy ‚Ü¶ by simpa using hy‚ü©
  ¬∑ ext; simp
  ¬∑ simpa [ContinuousAt] using (cts_add.1 x‚ÇÄ).continuousAt (x := (0 : R))
  ¬∑ simpa [ContinuousAt] using (cts_add.1 (-x‚ÇÄ)).continuousAt (x := x‚ÇÄ)

instance (priority := low) : IsTopologicalRing R :=
  letI := IsTopologicalAddGroup.toUniformSpace R
  letI := isUniformAddGroup_of_addCommGroup (G := R)
  inferInstance

theorem isOpen_ball (r : ValueGroupWithZero R) :
    IsOpen {x | v x < r} := by
  rw [isOpen_iff_mem_nhds]
  rcases eq_or_ne r 0 with rfl | hr
  ¬∑ simp
  ¬∑ intro x hx
    rw [mem_nhds_iff']
    simp only [setOf_subset_setOf]
    exact ‚ü®Units.mk0 _ hr,
      fun y hy => (sub_add_cancel y x).symm ‚ñ∏ ((v).map_add _ x).trans_lt (max_lt hy hx)‚ü©

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.isOpen_ball := isOpen_ball

theorem isClosed_ball (r : ValueGroupWithZero R) :
    IsClosed {x | v x < r} := by
  rcases eq_or_ne r 0 with rfl | hr
  ¬∑ simp
  ¬∑ exact AddSubgroup.isClosed_of_isOpen (Valuation.ltAddSubgroup v (Units.mk0 r hr))
      (isOpen_ball _)

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.isClosed_ball := isClosed_ball

theorem isClopen_ball (r : ValueGroupWithZero R) :
    IsClopen {x | v x < r} :=
  ‚ü®isClosed_ball _, isOpen_ball _‚ü©

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.isClopen_ball := isClopen_ball

lemma isOpen_closedBall {r : ValueGroupWithZero R} (hr : r ‚â† 0) :
    IsOpen {x | v x ‚â§ r} := by
  rw [isOpen_iff_mem_nhds]
  intro x hx
  rw [mem_nhds_iff']
  simp only [setOf_subset_setOf]
  exact ‚ü®Units.mk0 _ hr, fun y hy => (sub_add_cancel y x).symm ‚ñ∏
    le_trans ((v).map_add _ _) (max_le (le_of_lt hy) hx)‚ü©

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.isOpen_closedBall := isOpen_closedBall

theorem isClosed_closedBall (r : ValueGroupWithZero R) :
    IsClosed {x | v x ‚â§ r} := by
  rw [‚Üê isOpen_compl_iff, isOpen_iff_mem_nhds]
  intro x hx
  simp only [mem_compl_iff, mem_setOf_eq, not_le] at hx
  rw [mem_nhds_iff']
  have hx' : v x ‚â† 0 := ne_of_gt <| lt_of_le_of_lt zero_le' <| hx
  exact ‚ü®Units.mk0 _ hx', fun y hy hy' => ne_of_lt hy <| Valuation.map_sub_swap v x y ‚ñ∏
      (Valuation.map_sub_eq_of_lt_left _ <| lt_of_le_of_lt hy' hx)‚ü©

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.isClosed_closedBall := isClosed_closedBall

theorem isClopen_closedBall {r : ValueGroupWithZero R} (hr : r ‚â† 0) :
    IsClopen {x | v x ‚â§ r} :=
  ‚ü®isClosed_closedBall _, isOpen_closedBall hr‚ü©

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.isClopen_closedBall := isClopen_closedBall

theorem isClopen_sphere {r : ValueGroupWithZero R} (hr : r ‚â† 0) :
    IsClopen {x | v x = r} := by
  have h : {x : R | v x = r} = {x | v x ‚â§ r} \ {x | v x < r} := by
    ext x
    simp [‚Üê le_antisymm_iff]
  rw [h]
  exact IsClopen.diff (isClopen_closedBall hr) (isClopen_ball _)

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.isClopen_sphere := isClopen_sphere

lemma isOpen_sphere {r : ValueGroupWithZero R} (hr : r ‚â† 0) :
    IsOpen {x | v x = r} :=
  isClopen_sphere hr |>.isOpen

@[deprecated (since := "2025-08-01")]
alias _root_.ValuativeTopology.isOpen_sphere := isOpen_sphere

end

section

/-! # Alternate constructors -/

variable [TopologicalSpace R] [ContinuousConstVAdd R R]
  [TopologicalSpace F] [ContinuousConstVAdd F F]

/-- A "metatheorem" saying that if we proved that a valuative topology has a certain basis of
`nhds 0`, then any topology having the same basis of `nhds 0` which is also `ContinuousConstVAdd` is
automatically an `IsValuativeTopology`. -/
theorem of_hasBasis {R : Type*} [CommRing R] [ValuativeRel R]
    {Œπ : Type*} (p : Œπ ‚Üí Prop) (s : Œπ ‚Üí Set R)
    (ih : ‚àÄ [TopologicalSpace R] [IsValuativeTopology R], (nhds 0).HasBasis p s)
    [œÑ : TopologicalSpace R] [ContinuousConstVAdd R R] (h : (nhds 0).HasBasis p s) :
    IsValuativeTopology R := by
  rw [isValuativeTopology_iff_subgroups_basis_topology_eq]
  let := (valuation R).subgroups_basis
  specialize @ih this.topology of_subgroups_basis
  refine ext_nhds fun x ‚Ü¶ Filter.ext fun t ‚Ü¶ ?_
  rw [‚Üê @vadd_mem_nhds_vadd_iff _ _ this.topology _ _ _ _ (-x),
    ‚Üê @vadd_mem_nhds_vadd_iff _ _ œÑ _ _ _ _ (-x),
    vadd_eq_add, neg_add_cancel, h.mem_iff, ih.mem_iff]

lemma of_hasBasis_ne_zero
    (h : (ùìù (0 : R)).HasBasis (¬∑ ‚â† 0) fun Œ≥ ‚Ü¶ { x | (valuation R) x < Œ≥ }) :
    IsValuativeTopology R :=
  of_hasBasis _ _ (hasBasis_nhds_zero_ne_zero R) h

lemma of_hasBasis_ne_zero_and_le_one
    (h : (ùìù (0 : R)).HasBasis (fun Œ≥ ‚Ü¶ Œ≥ ‚â† 0 ‚àß Œ≥ ‚â§ 1) fun Œ≥ ‚Ü¶ { x | (valuation R) x < Œ≥ }) :
    IsValuativeTopology R :=
  of_hasBasis _ _ (hasBasis_nhds_zero_ne_zero_and_le_one R) h

lemma of_hasBasis_pair
    (h : (ùìù (0 : R)).HasBasis (fun rs : R √ó R ‚Ü¶ rs.1 ‚àà posSubmonoid R ‚àß rs.2 ‚àà posSubmonoid R)
      fun rs  ‚Ü¶ { x | x * rs.2 <·µ• rs.1 }) :
    IsValuativeTopology R :=
  of_hasBasis _ _ (hasBasis_nhds_zero_pair R) h

lemma of_hasBasis_compatible {v' : Valuation R Œì‚ÇÄ} [v'.Compatible]
    (h : (ùìù (0 : R)).HasBasis (fun rs : R √ó R ‚Ü¶ v' rs.1 ‚â† 0 ‚àß v' rs.2 ‚â† 0)
    fun rs : R √ó R ‚Ü¶ { x | v' x * v' rs.2 < v' rs.1 }) :
    IsValuativeTopology R :=
  of_hasBasis _ _ (hasBasis_nhds_zero_compatible v') h

lemma of_hasBasis_compatible_min_inv {v' : Valuation R Œì‚ÇÄ'} [v'.Compatible]
    (h : (ùìù (0 : R)).HasBasis (fun r : R ‚Ü¶ v' r ‚â† 0)
      fun r ‚Ü¶ { x | v' x < min (v' r) (v' r)‚Åª¬π }) :
    IsValuativeTopology R :=
  of_hasBasis _ _ (hasBasis_nhds_zero_compatible_min_inv v') h

lemma of_hasBasis_min_inv
    (h : (ùìù (0 : R)).HasBasis (fun r : R ‚Ü¶ r ‚àà posSubmonoid R)
      fun r ‚Ü¶ { x | x <·µ• r ‚àß x * r <·µ• 1 }) :
    IsValuativeTopology R :=
  of_hasBasis _ _ (hasBasis_nhds_zero_lt_and_mul_lt_one R) h

lemma of_hasBasis_compatible_ne_zero {v' : Valuation F Œì‚ÇÄ'} [v'.Compatible]
    (h : (ùìù (0 : F)).HasBasis (fun r ‚Ü¶ r ‚â† 0) fun r ‚Ü¶ { x | v' x < v' r }) :
    IsValuativeTopology F :=
  of_hasBasis _ _ (hasBasis_nhds_zero_compatible_ne_zero v') h

lemma of_hasBasis_ne_zero'
    (h : (ùìù (0 : F)).HasBasis (¬∑ ‚â† 0) fun r ‚Ü¶ { x | x <·µ• r }) :
    IsValuativeTopology F :=
  of_hasBasis _ _ (hasBasis_nhds_zero_ne_zero' F) h

lemma of_hasBasis_compatible_ne_zero_and_le_one {v' : Valuation F Œì‚ÇÄ'} [v'.Compatible]
    (h : (ùìù (0 : F)).HasBasis (fun r ‚Ü¶ r ‚â† 0 ‚àß v' r ‚â§ 1) fun r ‚Ü¶ { x | v' x < v' r }) :
    IsValuativeTopology F :=
  of_hasBasis _ _ (hasBasis_nhds_zero_compatible_ne_zero_and_le_one v') h

lemma of_hasBasis_ne_zero_and_le_one'
    (h : (ùìù (0 : F)).HasBasis (fun r ‚Ü¶ r ‚â† 0 ‚àß r ‚â§·µ• 1) fun r ‚Ü¶ { x | x <·µ• r }) :
    IsValuativeTopology F :=
  of_hasBasis _ _ (hasBasis_nhds_zero_ne_zero_and_le_one' F) h

end

end IsValuativeTopology

namespace ValuativeRel

@[inherit_doc]
scoped notation "ùí™[" R "]" => Valuation.integer (valuation R)

@[inherit_doc]
scoped notation "ùìÇ[" K "]" => IsLocalRing.maximalIdeal ùí™[K]

@[inherit_doc]
scoped notation "ùìÄ[" K "]" => IsLocalRing.ResidueField ùí™[K]

end ValuativeRel
