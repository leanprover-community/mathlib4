/-
Copyright (c) 2021 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
import Mathlib.Topology.Algebra.Valued.ValuationTopology
import Mathlib.Topology.Algebra.WithZeroTopology
import Mathlib.Topology.Algebra.UniformField

/-!
# Valued fields and their completions

In this file we study the topology of a field `K` endowed with a valuation (in our application
to adic spaces, `K` will be the valuation field associated to some valuation on a ring, defined in
valuation.basic).

We already know from valuation.topology that one can build a topology on `K` which
makes it a topological ring.

The first goal is to show `K` is a topological *field*, i.e. inversion is continuous
at every non-zero element.

The next goal is to prove `K` is a *completable* topological field. This gives us
a completion `hat K` which is a topological field. We also prove that `K` is automatically
separated, so the map from `K` to `hat K` is injective.

Then we extend the valuation given on `K` to a valuation on `hat K`.
-/


open Filter Set

open Topology

section DivisionRing

variable {K : Type*} [DivisionRing K] {Œì‚ÇÄ : Type*} [LinearOrderedCommGroupWithZero Œì‚ÇÄ]

section ValuationTopologicalDivisionRing

section InversionEstimate

variable (v : Valuation K Œì‚ÇÄ)

-- The following is the main technical lemma ensuring that inversion is continuous
-- in the topology induced by a valuation on a division ring (i.e. the next instance)
-- and the fact that a valued field is completable
-- [BouAC, VI.5.1 Lemme 1]
theorem Valuation.inversion_estimate {x y : K} {Œ≥ : Œì‚ÇÄÀ£} (y_ne : y ‚â† 0)
    (h : v (x - y) < min (Œ≥ * (v y * v y)) (v y)) : v (x‚Åª¬π - y‚Åª¬π) < Œ≥ := by
  have hyp1 : v (x - y) < Œ≥ * (v y * v y) := lt_of_lt_of_le h (min_le_left _ _)
  have hyp1' : v (x - y) * (v y * v y)‚Åª¬π < Œ≥ := mul_inv_lt_of_lt_mul‚ÇÄ hyp1
  have hyp2 : v (x - y) < v y := lt_of_lt_of_le h (min_le_right _ _)
  have key : v x = v y := Valuation.map_eq_of_sub_lt v hyp2
  have x_ne : x ‚â† 0 := by
    intro h
    apply y_ne
    rw [h, v.map_zero] at key
    exact v.zero_iff.1 key.symm
  have decomp : x‚Åª¬π - y‚Åª¬π = x‚Åª¬π * (y - x) * y‚Åª¬π := by
    rw [mul_sub_left_distrib, sub_mul, mul_assoc, show y * y‚Åª¬π = 1 from mul_inv_cancel‚ÇÄ y_ne,
      show x‚Åª¬π * x = 1 from inv_mul_cancel‚ÇÄ x_ne, mul_one, one_mul]
  calc
    v (x‚Åª¬π - y‚Åª¬π) = v (x‚Åª¬π * (y - x) * y‚Åª¬π) := by rw [decomp]
    _ = v x‚Åª¬π * (v <| y - x) * v y‚Åª¬π := by repeat' rw [Valuation.map_mul]
    _ = (v x)‚Åª¬π * (v <| y - x) * (v y)‚Åª¬π := by rw [map_inv‚ÇÄ, map_inv‚ÇÄ]
    _ = (v <| y - x) * (v y * v y)‚Åª¬π := by rw [mul_assoc, mul_comm, key, mul_assoc, mul_inv_rev]
    _ = (v <| y - x) * (v y * v y)‚Åª¬π := rfl
    _ = (v <| x - y) * (v y * v y)‚Åª¬π := by rw [Valuation.map_sub_swap]
    _ < Œ≥ := hyp1'

theorem Valuation.inversion_estimate' {x y r s : K} (y_ne : y ‚â† 0) (hr : r ‚â† 0) (hs : s ‚â† 0)
    (h : v (x - y) < min ((v s / v r) * (v y * v y)) (v y)) : v (x‚Åª¬π - y‚Åª¬π) * v r < v s := by
  have hr' : 0 < v r := by simp [zero_lt_iff, hr]
  let Œ≥ : Œì‚ÇÄÀ£ := .mk0 (v s / v r) (by simp [hs, hr])
  calc
    v (x‚Åª¬π - y‚Åª¬π) * v r < Œ≥ * v r := by gcongr; exact Valuation.inversion_estimate v y_ne h
    _ = v s := div_mul_cancel‚ÇÄ _ (by simpa)

end InversionEstimate

open Valued

/-- The topology coming from a valuation on a division ring makes it a topological division ring
[BouAC, VI.5.1 middle of Proposition 1] -/
instance (priority := 100) Valued.isTopologicalDivisionRing [Valued K Œì‚ÇÄ] :
    IsTopologicalDivisionRing K :=
  { (by infer_instance : IsTopologicalRing K) with
    continuousAt_inv‚ÇÄ := by
      intro x x_ne s s_in
      obtain ‚ü®Œ≥, hs‚ü© := Valued.mem_nhds.mp s_in; clear s_in
      rw [mem_map, Valued.mem_nhds]
      change ‚àÉ Œ≥ : Œì‚ÇÄÀ£, { y : K | (v (y - x) : Œì‚ÇÄ) < Œ≥ } ‚äÜ { x : K | x‚Åª¬π ‚àà s }
      have vx_ne := (Valuation.ne_zero_iff <| v).mpr x_ne
      let Œ≥' := Units.mk0 _ vx_ne
      use min (Œ≥ * (Œ≥' * Œ≥')) Œ≥'
      intro y y_in
      apply hs
      simp only [mem_setOf_eq] at y_in
      rw [Units.min_val, Units.val_mul, Units.val_mul] at y_in
      exact Valuation.inversion_estimate _ x_ne y_in }

/-- A valued division ring is separated. -/
instance (priority := 100) ValuedRing.separated [Valued K Œì‚ÇÄ] : T0Space K := by
  suffices T2Space K by infer_instance
  apply IsTopologicalAddGroup.t2Space_of_zero_sep
  intro x x_ne
  refine ‚ü®{ k | v k < v x }, ?_, fun h => lt_irrefl _ h‚ü©
  rw [Valued.mem_nhds]
  have vx_ne := (Valuation.ne_zero_iff <| v).mpr x_ne
  let Œ≥' := Units.mk0 _ vx_ne
  exact ‚ü®Œ≥', fun y hy => by simpa using hy‚ü©

section

open WithZeroTopology

open Valued

theorem Valued.continuous_valuation [Valued K Œì‚ÇÄ] : Continuous (v : K ‚Üí Œì‚ÇÄ) := by
  rw [continuous_iff_continuousAt]
  intro x
  rcases eq_or_ne x 0 with (rfl | h)
  ¬∑ rw [ContinuousAt, map_zero, WithZeroTopology.tendsto_zero]
    intro Œ≥ hŒ≥
    rw [Filter.Eventually, Valued.mem_nhds_zero]
    use Units.mk0 Œ≥ hŒ≥; rfl
  ¬∑ have v_ne : (v x : Œì‚ÇÄ) ‚â† 0 := (Valuation.ne_zero_iff _).mpr h
    rw [ContinuousAt, WithZeroTopology.tendsto_of_ne_zero v_ne]
    apply Valued.loc_const v_ne

end

end ValuationTopologicalDivisionRing

end DivisionRing

namespace Valued

open UniformSpace

variable {K : Type*} [Field K] {Œì‚ÇÄ : Type*} [LinearOrderedCommGroupWithZero Œì‚ÇÄ] [hv : Valued K Œì‚ÇÄ]

local notation "hat " => Completion

/-- A valued field is completable. -/
instance (priority := 100) completable : CompletableTopField K :=
  { ValuedRing.separated with
    nice := by
      rintro F hF h0
      have : ‚àÉ Œ≥‚ÇÄ : Œì‚ÇÄÀ£, ‚àÉ M ‚àà F, ‚àÄ x ‚àà M, (Œ≥‚ÇÄ : Œì‚ÇÄ) ‚â§ v x := by
        rcases Filter.inf_eq_bot_iff.mp h0 with ‚ü®U, U_in, M, M_in, H‚ü©
        rcases Valued.mem_nhds_zero.mp U_in with ‚ü®Œ≥‚ÇÄ, hU‚ü©
        exists Œ≥‚ÇÄ, M, M_in
        intro x xM
        apply le_of_not_gt _
        intro hyp
        have : x ‚àà U ‚à© M := ‚ü®hU hyp, xM‚ü©
        rwa [H] at this
      rcases this with ‚ü®Œ≥‚ÇÄ, M‚ÇÄ, M‚ÇÄ_in, H‚ÇÄ‚ü©
      rw [Valued.cauchy_iff] at hF ‚ä¢
      refine ‚ü®hF.1.map _, ?_‚ü©
      replace hF := hF.2
      intro Œ≥
      rcases hF (min (Œ≥ * Œ≥‚ÇÄ * Œ≥‚ÇÄ) Œ≥‚ÇÄ) with ‚ü®M‚ÇÅ, M‚ÇÅ_in, H‚ÇÅ‚ü©
      clear hF
      use (fun x : K => x‚Åª¬π) '' (M‚ÇÄ ‚à© M‚ÇÅ)
      constructor
      ¬∑ rw [mem_map]
        apply mem_of_superset (Filter.inter_mem M‚ÇÄ_in M‚ÇÅ_in)
        exact subset_preimage_image _ _
      ¬∑ rintro _ ‚ü®x, ‚ü®x_in‚ÇÄ, x_in‚ÇÅ‚ü©, rfl‚ü© _ ‚ü®y, ‚ü®_, y_in‚ÇÅ‚ü©, rfl‚ü©
        simp only
        specialize H‚ÇÅ x x_in‚ÇÅ y y_in‚ÇÅ
        replace x_in‚ÇÄ := H‚ÇÄ x x_in‚ÇÄ
        clear H‚ÇÄ
        apply Valuation.inversion_estimate
        ¬∑ have : (v x : Œì‚ÇÄ) ‚â† 0 := by
            intro h
            rw [h] at x_in‚ÇÄ
            simp at x_in‚ÇÄ
          exact (Valuation.ne_zero_iff _).mp this
        ¬∑ refine lt_of_lt_of_le H‚ÇÅ ?_
          grw [Units.min_val, mul_assoc, Units.val_mul, Units.val_mul, x_in‚ÇÄ] }

open WithZeroTopology

lemma valuation_isClosedMap : IsClosedMap (v : K ‚Üí Œì‚ÇÄ) := by
  refine IsClosedMap.of_nonempty ?_
  intro U hU hU'
  simp only [‚Üê isOpen_compl_iff, isOpen_iff_mem_nhds, mem_compl_iff, mem_nhds, subset_compl_comm,
    compl_setOf, not_lt] at hU
  simp only [isClosed_iff, mem_image, map_eq_zero, exists_eq_right, ne_eq, image_subset_iff]
  refine (em _).imp_right fun h ‚Ü¶ ?_
  obtain ‚ü®Œ≥, h‚ü© := hU _ h
  simp only [sub_zero] at h
  refine ‚ü®Œ≥, Œ≥.ne_zero, h.trans ?_‚ü©
  intro
  simp

/-- The extension of the valuation of a valued field to the completion of the field. -/
noncomputable def extension : hat K ‚Üí Œì‚ÇÄ :=
  Completion.isDenseInducing_coe.extend (v : K ‚Üí Œì‚ÇÄ)

theorem continuous_extension : Continuous (Valued.extension : hat K ‚Üí Œì‚ÇÄ) := by
  refine Completion.isDenseInducing_coe.continuous_extend ?_
  intro x‚ÇÄ
  rcases eq_or_ne x‚ÇÄ 0 with (rfl | h)
  ¬∑ refine ‚ü®0, ?_‚ü©
    erw [‚Üê Completion.isDenseInducing_coe.isInducing.nhds_eq_comap]
    exact Valued.continuous_valuation.tendsto' 0 0 (map_zero v)
  ¬∑ have preimage_one : v ‚Åª¬π' {(1 : Œì‚ÇÄ)} ‚àà ùìù (1 : K) := by
      have : (v (1 : K) : Œì‚ÇÄ) ‚â† 0 := by
        rw [Valuation.map_one]
        exact zero_ne_one.symm
      convert Valued.loc_const this
      ext x
      rw [Valuation.map_one, mem_preimage, mem_singleton_iff, mem_setOf_eq]
    obtain ‚ü®V, V_in, hV‚ü© : ‚àÉ V ‚àà ùìù (1 : hat K), ‚àÄ x : K, (x : hat K) ‚àà V ‚Üí (v x : Œì‚ÇÄ) = 1 := by
      rwa [Completion.isDenseInducing_coe.nhds_eq_comap, mem_comap] at preimage_one
    have : ‚àÉ V' ‚àà ùìù (1 : hat K), (0 : hat K) ‚àâ V' ‚àß ‚àÄ (x) (_ : x ‚àà V') (y) (_ : y ‚àà V'),
      x * y‚Åª¬π ‚àà V := by
      have : Tendsto (fun p : hat K √ó hat K => p.1 * p.2‚Åª¬π) ((ùìù 1) √óÀ¢ (ùìù 1)) (ùìù 1) := by
        rw [‚Üê nhds_prod_eq]
        conv =>
          congr
          rfl
          rfl
          rw [‚Üê one_mul (1 : hat K)]
        refine
          Tendsto.mul continuous_fst.continuousAt (Tendsto.comp ?_ continuous_snd.continuousAt)
        convert (continuousAt_inv‚ÇÄ (zero_ne_one.symm : 1 ‚â† (0 : hat K))).tendsto
        exact inv_one.symm
      rcases tendsto_prod_self_iff.mp this V V_in with ‚ü®U, U_in, hU‚ü©
      let hatKstar := ({0}·∂ú : Set <| hat K)
      have : hatKstar ‚àà ùìù (1 : hat K) := compl_singleton_mem_nhds zero_ne_one.symm
      use U ‚à© hatKstar, Filter.inter_mem U_in this
      constructor
      ¬∑ rintro ‚ü®_, h'‚ü©
        rw [mem_compl_singleton_iff] at h'
        exact h' rfl
      ¬∑ rintro x ‚ü®hx, _‚ü© y ‚ü®hy, _‚ü©
        apply hU <;> assumption
    rcases this with ‚ü®V', V'_in, zeroV', hV'‚ü©
    have nhds_right : (fun x => x * x‚ÇÄ) '' V' ‚àà ùìù x‚ÇÄ := by
      have l : Function.LeftInverse (fun x : hat K => x * x‚ÇÄ‚Åª¬π) fun x : hat K => x * x‚ÇÄ := by
        intro x
        simp only [mul_assoc, mul_inv_cancel‚ÇÄ h, mul_one]
      have r : Function.RightInverse (fun x : hat K => x * x‚ÇÄ‚Åª¬π) fun x : hat K => x * x‚ÇÄ := by
        intro x
        simp only [mul_assoc, inv_mul_cancel‚ÇÄ h, mul_one]
      have c : Continuous fun x : hat K => x * x‚ÇÄ‚Åª¬π := continuous_id.mul continuous_const
      rw [image_eq_preimage_of_inverse l r]
      rw [‚Üê mul_inv_cancel‚ÇÄ h] at V'_in
      exact c.continuousAt V'_in
    have : ‚àÉ z‚ÇÄ : K, ‚àÉ y‚ÇÄ ‚àà V', ‚Üëz‚ÇÄ = y‚ÇÄ * x‚ÇÄ ‚àß z‚ÇÄ ‚â† 0 := by
      rcases Completion.denseRange_coe.mem_nhds nhds_right with ‚ü®z‚ÇÄ, y‚ÇÄ, y‚ÇÄ_in, H : y‚ÇÄ * x‚ÇÄ = z‚ÇÄ‚ü©
      refine ‚ü®z‚ÇÄ, y‚ÇÄ, y‚ÇÄ_in, ‚ü®H.symm, ?_‚ü©‚ü©
      rintro rfl
      exact mul_ne_zero (ne_of_mem_of_not_mem y‚ÇÄ_in zeroV') h H
    rcases this with ‚ü®z‚ÇÄ, y‚ÇÄ, y‚ÇÄ_in, hz‚ÇÄ, z‚ÇÄ_ne‚ü©
    have vz‚ÇÄ_ne : (v z‚ÇÄ : Œì‚ÇÄ) ‚â† 0 := by rwa [Valuation.ne_zero_iff]
    refine ‚ü®v z‚ÇÄ, ?_‚ü©
    rw [WithZeroTopology.tendsto_of_ne_zero vz‚ÇÄ_ne, eventually_comap]
    filter_upwards [nhds_right] with x x_in a ha
    rcases x_in with ‚ü®y, y_in, rfl‚ü©
    have : (v (a * z‚ÇÄ‚Åª¬π) : Œì‚ÇÄ) = 1 := by
      apply hV
      have : (z‚ÇÄ‚Åª¬π : K) = (z‚ÇÄ : hat K)‚Åª¬π := map_inv‚ÇÄ (Completion.coeRingHom : K ‚Üí+* hat K) z‚ÇÄ
      rw [Completion.coe_mul, this, ha, hz‚ÇÄ, mul_inv, mul_comm y‚ÇÄ‚Åª¬π, ‚Üê mul_assoc, mul_assoc y,
        mul_inv_cancel‚ÇÄ h, mul_one]
      solve_by_elim
    calc
      v a = v (a * z‚ÇÄ‚Åª¬π * z‚ÇÄ) := by rw [mul_assoc, inv_mul_cancel‚ÇÄ z‚ÇÄ_ne, mul_one]
      _ = v (a * z‚ÇÄ‚Åª¬π) * v z‚ÇÄ := Valuation.map_mul _ _ _
      _ = v z‚ÇÄ := by rw [this, one_mul]

@[simp, norm_cast]
theorem extension_extends (x : K) : extension (x : hat K) = v x := by
  refine Completion.isDenseInducing_coe.extend_eq_of_tendsto ?_
  rw [‚Üê Completion.isDenseInducing_coe.nhds_eq_comap]
  exact Valued.continuous_valuation.continuousAt

/-- the extension of a valuation on a division ring to its completion. -/
noncomputable def extensionValuation : Valuation (hat K) Œì‚ÇÄ where
  toFun := Valued.extension
  map_zero' := by
    rw [‚Üê v.map_zero (R := K), ‚Üê Valued.extension_extends (0 : K)]
    rfl
  map_one' := by
    rw [‚Üê Completion.coe_one, Valued.extension_extends (1 : K)]
    exact Valuation.map_one _
  map_mul' x y := by
    apply Completion.induction_on‚ÇÇ x y
      (p := fun x y => extension (x * y) = extension x * extension y)
    ¬∑ have c1 : Continuous fun x : hat K √ó hat K => Valued.extension (x.1 * x.2) :=
        Valued.continuous_extension.comp (continuous_fst.mul continuous_snd)
      have c2 : Continuous fun x : hat K √ó hat K => Valued.extension x.1 * Valued.extension x.2 :=
        (Valued.continuous_extension.comp continuous_fst).mul
          (Valued.continuous_extension.comp continuous_snd)
      exact isClosed_eq c1 c2
    ¬∑ intro x y
      norm_cast
      exact Valuation.map_mul _ _ _
  map_add_le_max' x y := by
    rw [le_max_iff]
    apply Completion.induction_on‚ÇÇ x y
      (p := fun x y => extension (x + y) ‚â§ extension x ‚à® extension (x + y) ‚â§ extension y)
    ¬∑ have cont : Continuous (Valued.extension : hat K ‚Üí Œì‚ÇÄ) := Valued.continuous_extension
      exact
        (isClosed_le (cont.comp continuous_add) <| cont.comp continuous_fst).union
          (isClosed_le (cont.comp continuous_add) <| cont.comp continuous_snd)
    ¬∑ intro x y
      norm_cast
      rw [‚Üê le_max_iff]
      exact v.map_add x y

@[simp]
lemma extensionValuation_apply_coe (x : K) :
    Valued.extensionValuation (x : hat K) = v x :=
  extension_extends x

@[simp]
lemma extension_eq_zero_iff {x : hat K} :
    extension x = 0 ‚Üî x = 0 := by
  suffices extensionValuation x = 0 ‚Üî x = 0 from this
  simp

lemma continuous_extensionValuation : Continuous (Valued.extensionValuation : hat K ‚Üí Œì‚ÇÄ) :=
  continuous_extension

lemma exists_coe_eq_v (x : hat K) : ‚àÉ r : K, extensionValuation x = v r := by
  rcases eq_or_ne x 0 with (rfl | h)
  ¬∑ use 0
    exact extensionValuation_apply_coe 0
  ¬∑ refine Completion.denseRange_coe.induction_on x ?_ (by simp)
    simpa [eq_comm] using
      valuation_isClosedMap.isClosed_range.preimage continuous_extensionValuation

-- Bourbaki CA VI ¬ß5 no.3 Proposition 5 (d)
theorem closure_coe_completion_v_lt {Œ≥ : Œì‚ÇÄÀ£} :
    closure ((‚Üë) '' { x : K | v x < (Œ≥ : Œì‚ÇÄ) }) =
    { x : hat K | extensionValuation x < (Œ≥ : Œì‚ÇÄ) } := by
  ext x
  let Œ≥‚ÇÄ := extensionValuation x
  suffices Œ≥‚ÇÄ ‚â† 0 ‚Üí (x ‚àà closure ((‚Üë) '' { x : K | v x < (Œ≥ : Œì‚ÇÄ) }) ‚Üî Œ≥‚ÇÄ < (Œ≥ : Œì‚ÇÄ)) by
    rcases eq_or_ne Œ≥‚ÇÄ 0 with h | h
    ¬∑ simp only [(Valuation.zero_iff _).mp h, mem_setOf_eq, Valuation.map_zero, Units.zero_lt,
        iff_true]
      apply subset_closure
      exact ‚ü®0, by simp only [mem_setOf_eq, Valuation.map_zero, Units.zero_lt, true_and]; rfl‚ü©
    ¬∑ exact this h
  intro h
  have hŒ≥‚ÇÄ : extension ‚Åª¬π' {Œ≥‚ÇÄ} ‚àà ùìù x :=
    continuous_extension.continuousAt.preimage_mem_nhds
      (WithZeroTopology.singleton_mem_nhds_of_ne_zero h)
  rw [mem_closure_iff_nhds']
  refine ‚ü®fun hx => ?_, fun hx s hs => ?_‚ü©
  ¬∑ obtain ‚ü®‚ü®-, y, hy‚ÇÅ : v y < (Œ≥ : Œì‚ÇÄ), rfl‚ü©, hy‚ÇÇ‚ü© := hx _ hŒ≥‚ÇÄ
    replace hy‚ÇÇ : v y = Œ≥‚ÇÄ := by simpa using hy‚ÇÇ
    rwa [‚Üê hy‚ÇÇ]
  ¬∑ obtain ‚ü®y, hy‚ÇÅ, hy‚ÇÇ‚ü© := Completion.denseRange_coe.mem_nhds (inter_mem hŒ≥‚ÇÄ hs)
    replace hy‚ÇÅ : v y = Œ≥‚ÇÄ := by simpa using hy‚ÇÅ
    rw [‚Üê hy‚ÇÅ] at hx
    exact ‚ü®‚ü®y, ‚ü®y, hx, rfl‚ü©‚ü©, hy‚ÇÇ‚ü©

theorem closure_coe_completion_v_mul_v_lt {r s : K} (hr : r ‚â† 0) (hs : s ‚â† 0) :
    closure ((‚Üë) '' { x : K | v x * v r < v s }) =
    { x : hat K | extensionValuation x * v r < v s } := by
  have hrs : v s / v r ‚â† 0 := by simp [hr, hs]
  convert closure_coe_completion_v_lt (Œ≥ := .mk0 _ hrs) using 3
  all_goals simp [‚Üê lt_div_iff‚ÇÄ, zero_lt_iff, hr]

noncomputable instance valuedCompletion : Valued (hat K) Œì‚ÇÄ where
  v := extensionValuation
  is_topological_valuation s := by
    suffices
      HasBasis (ùìù (0 : hat K)) (fun _ => True) fun Œ≥ : Œì‚ÇÄÀ£ => { x | extensionValuation x < Œ≥ } by
      rw [this.mem_iff]
      exact exists_congr fun Œ≥ => by simp
    simp_rw [‚Üê closure_coe_completion_v_lt]
    exact (hasBasis_nhds_zero K Œì‚ÇÄ).hasBasis_of_isDenseInducing Completion.isDenseInducing_coe

@[simp]
theorem valuedCompletion_apply (x : K) : Valued.v (x : hat K) = v x :=
  extension_extends x

lemma valuedCompletion_surjective_iff :
    Function.Surjective (v : hat K ‚Üí Œì‚ÇÄ) ‚Üî Function.Surjective (v : K ‚Üí Œì‚ÇÄ) := by
  constructor <;> intro h Œ≥ <;> obtain ‚ü®a, ha‚ü© := h Œ≥
  ¬∑ induction a using Completion.induction_on
    ¬∑ by_cases H : ‚àÉ x : K, (v : K ‚Üí Œì‚ÇÄ) x = Œ≥
      ¬∑ simp [H]
      ¬∑ simp only [H, imp_false]
        rcases eq_or_ne Œ≥ 0 with rfl | hŒ≥
        ¬∑ simp at H
        ¬∑ convert isClosed_univ.sdiff (isOpen_sphere (hat K) hŒ≥) using 1
          ext x
          simp
    ¬∑ exact ‚ü®_, by simpa using ha‚ü©
  ¬∑ exact ‚ü®a, by simp [ha]‚ü©

instance {R : Type*} [CommSemiring R] [Algebra R K] [UniformContinuousConstSMul R K]
    [FaithfulSMul R K] : FaithfulSMul R (hat K) := by
  rw [faithfulSMul_iff_algebraMap_injective R (hat K)]
  exact (FaithfulSMul.algebraMap_injective K (hat K)).comp (FaithfulSMul.algebraMap_injective R K)

end Valued

section Notation

namespace Valued

variable (K : Type*) [Field K] {Œì‚ÇÄ : outParam Type*}
    [LinearOrderedCommGroupWithZero Œì‚ÇÄ] [vK : Valued K Œì‚ÇÄ]

/-- A `Valued` version of `Valuation.integer`, enabling the notation `ùí™[K]` for the
valuation integers of a valued field `K`. -/
@[reducible]
def integer : Subring K := (vK.v).integer

@[inherit_doc]
scoped notation "ùí™[" K "]" => Valued.integer K

/-- An abbreviation for `IsLocalRing.maximalIdeal ùí™[K]` of a valued field `K`, enabling the notation
`ùìÇ[K]` for the maximal ideal in `ùí™[K]` of a valued field `K`. -/
@[reducible]
def maximalIdeal : Ideal ùí™[K] := IsLocalRing.maximalIdeal ùí™[K]

@[inherit_doc]
scoped notation "ùìÇ[" K "]" => maximalIdeal K

/-- An abbreviation for `IsLocalRing.ResidueField ùí™[K]` of a `Valued` instance, enabling the
notation `ùìÄ[K]` for the residue field of a valued field `K`. -/
@[reducible]
def ResidueField := IsLocalRing.ResidueField (ùí™[K])

@[inherit_doc]
scoped notation "ùìÄ[" K "]" => ResidueField K

end Valued

end Notation
