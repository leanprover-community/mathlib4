/-
Copyright (c) 2025 Yakov Pechersky. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yakov Pechersky
-/

import Mathlib.Analysis.SpecificLimits.Normed
import Mathlib.Topology.Algebra.InfiniteSum.Nonarchimedean
import Mathlib.Topology.Algebra.Valued.LocallyCompact


/-!

# Adic expansions of elements of rings of integers of nonarchimedean fields.

We show that every element of a ring of integers of a nonarchimedean field can be uniquely
determined as an expansion in terms of a fixed uniformizer.

## Main definitions

* `AdicExpansion.Digits`: a preimage of the residue field which is used in the expansion.
* `AdicExpansion`: the expansion itself, implemented as a type synonym of functions from `‚Ñï`.

## TODO
* `AdicExpansion.evalAt`: the evaluation of the expansion at a given point.
* Show that the induced metric space has the same topology regardless of uniformizer chosen.
* `AdicExpansion.appr`: the expansion for a given element of the ring of integers, when over a
  complete discrete valuation ring.

-/

variable {K R : Type*} [NontriviallyNormedField K] [IsUltrametricDist K] [CommRing R]

open scoped Valued NormedField

namespace AdicExpansion

variable [IsLocalRing R]

section Digits

variable (R) in
/-- The digits used in an adic expansion, requiring that they are in bijection with the
residue field. Zero is required to be a digit to ensure uniqueness of the expansion. -/
structure Digits where
  /-- The underlying preimage of the residue field. -/
  (carrier : Set R)
  (zero : 0 ‚àà carrier)
  (bij : Set.BijOn (IsLocalRing.residue R) carrier (Set.univ))

instance : SetLike (Digits R) R := ‚ü®Digits.carrier, by rintro ‚ü®‚ü© ‚ü®‚ü©; simp‚ü©

noncomputable
instance (digits : Digits R) : Zero digits := ‚ü®0, digits.zero‚ü©

instance (digits : Digits R) : Nonempty digits := ‚ü®0‚ü©

@[simp, norm_cast]
lemma Digits.coe_zero (digits : Digits R) : ((0 : digits) : R) = 0 := rfl

lemma Digits.ext_iff {digits : Digits R} {x y : digits} : x = y ‚Üî (x : R) = y := by
  simp

@[simp]
lemma Digits.isUnit_iff {digits : Digits R} {x : digits} :
    IsUnit (x : R) ‚Üî x ‚â† 0 := by
  rw [iff_not_comm]
  constructor <;> intro h
  ¬∑ simp [h]
  rw [Digits.ext_iff, Digits.coe_zero]
  exact digits.bij.injOn x.prop digits.zero (by simpa using h)

lemma Digits.not_isUnit_dvd_iff {digits : Digits R} {œñ : R} (hœñ : ¬¨ IsUnit œñ) {x : digits} :
    œñ ‚à£ x ‚Üî x = 0 := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ by simp [h]‚ü©
  rw [‚Üê not_not (a := x = 0), ‚Üê ne_eq, ‚Üê isUnit_iff]
  exact not_isUnit_of_not_isUnit_dvd hœñ h

lemma Digits.coe_sub_eq_iff {digits : Digits R} {x y : digits} :
    (x : R) - y = 0 ‚Üî x = y := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ by simp [h]‚ü©
  rw [Digits.ext_iff, ‚Üê sub_eq_zero, h]

lemma Digits.not_isUnit_dvd_sub_iff {digits : Digits R} {œñ : R} (hœñ : ¬¨ IsUnit œñ)
    {x y : digits} :
    œñ ‚à£ x - y ‚Üî x = y := by
  constructor
  ¬∑ rintro ‚ü®c, hc‚ü©
    apply_fun IsLocalRing.residue R at hc
    ext
    refine digits.bij.injOn x.prop y.prop ?_
    rw [‚Üê sub_eq_zero, ‚Üê map_sub, hc, (IsLocalRing.residue_eq_zero_iff _).mpr]
    simp [hœñ]
  ¬∑ intro h
    simp [h]

end Digits

end AdicExpansion

open AdicExpansion

/-- A formal expansion of an element in the local ring, at the digits specified. Meant to
be evaluated using `AdicExpansion.evalAtUpto` and related definitions. -/
def AdicExpansion [IsLocalRing R] (d : Digits R) := ‚Ñï ‚Üí d

namespace AdicExpansion

variable [IsLocalRing R] {D : Digits R}

protected lemma ext_iff {f g : AdicExpansion D} :
    f = g ‚Üî ‚àÄ n, f n = g n :=
  funext_iff

@[ext]
protected lemma ext {f g : AdicExpansion D} (h : ‚àÄ n, f n = g n) : f = g :=
  funext h

noncomputable
instance : Zero (AdicExpansion D) := inferInstanceAs (Zero (‚Ñï ‚Üí D))

@[simp]
lemma zero_apply (n : ‚Ñï) : (0 : AdicExpansion D) n = 0 := rfl

/-- Evaluation of an `AdicExpansion` up to the indicated power, using the provided "base". -/
noncomputable
def evalAtUpto (œñ : R) (f : AdicExpansion D) (n : ‚Ñï) : R :=
  ‚àë i ‚àà Finset.range n, f i * œñ ^ i

@[simp]
lemma evalAtUpto_zero (œñ : R) (f : AdicExpansion D) :
    evalAtUpto œñ f 0 = 0 := by
  simp only [evalAtUpto, Finset.sum_range_zero, zero_mul]

@[simp]
lemma evalAtUpto_one (œñ : R) (f : AdicExpansion D) :
    evalAtUpto œñ f 1 = f 0 := by
  simp [evalAtUpto]

lemma evalAtUpto_add_one (œñ : R) (f : AdicExpansion D) (n : ‚Ñï) :
    evalAtUpto œñ f (n + 1) = evalAtUpto œñ f n + f n * œñ ^ n := by
  simp only [evalAtUpto, Finset.sum_range_succ]

@[simp]
lemma zero_evalAtUpto (œñ : R) (n : ‚Ñï) :
    evalAtUpto œñ (0 : ‚Ñï ‚Üí D) n = 0 := by
  induction n with
  | zero => simp
  | succ n ih => simp [evalAtUpto_add_one, ih]

@[simp]
lemma evalAtUpto_at_zero (f : AdicExpansion D) (n : ‚Ñï) :
    evalAtUpto 0 f (n + 1) = f 0 := by
  induction n with
  | zero => simp
  | succ n ih =>
    rw [evalAtUpto_add_one, ih]
    simp

lemma congr_of_eqOn (œñ : R) {f g : AdicExpansion D} {n : ‚Ñï}
    (h : ‚àÄ i < n, f i = g i) :
    evalAtUpto œñ f n = evalAtUpto œñ g n := by
  induction n with
  | zero => simp
  | succ n ih =>
    simp only [evalAtUpto_add_one]
    rw [ih (fun i hi ‚Ü¶ h i (Nat.lt_succ_of_lt hi)), h _ (by simp)]

lemma mul_evalAtUpto_of_add_one (œñ : R) (f : AdicExpansion D) (n : ‚Ñï) :
    œñ * evalAtUpto œñ (f ‚àò (¬∑ + 1)) n = evalAtUpto œñ f (n + 1) - f 0 := by
  induction n with
  | zero => simp
  | succ n ih =>
    simp only [evalAtUpto_add_one, Function.comp_apply, mul_add, ih]
    ring

lemma evalAtUpto_add_one' (œñ : R) (f : AdicExpansion D) (n : ‚Ñï) :
    evalAtUpto œñ f (n + 1) = f 0 + œñ * evalAtUpto œñ (f ‚àò (¬∑ + 1)) n := by
  rw [mul_evalAtUpto_of_add_one]
  ring

lemma dvd_evalAtUpto_iff_apply_zero {œñ : R} (hœñ : ¬¨ IsUnit œñ)
    {f : AdicExpansion D} {n : ‚Ñï} :
    œñ ‚à£ evalAtUpto œñ f (n + 1) ‚Üî f 0 = 0 := by
  constructor
  ¬∑ intro h
    induction n with
    | zero => simpa using not_isUnit_of_not_isUnit_dvd hœñ h
    | succ n ih =>
      obtain ‚ü®c, h‚ü© := h
      rw [evalAtUpto_add_one, eq_comm, ‚Üê sub_eq_iff_eq_add, pow_succ', mul_left_comm,
        ‚Üê mul_sub] at h
      exact ih ‚ü®_, h.symm‚ü©
  ¬∑ intro h
    refine ‚ü®evalAtUpto œñ (f ‚àò (¬∑ + 1)) n, ?_‚ü©
    simp [mul_evalAtUpto_of_add_one, h]

lemma evalAtUpto_eq_zero_iff [IsDomain R] {œñ : R} (hœñ : ¬¨ IsUnit œñ) (hn : œñ ‚â† 0)
    {f : AdicExpansion D} {n : ‚Ñï} :
    evalAtUpto œñ f (n + 1) = 0 ‚Üî ‚àÄ i < n + 1, f i = 0 := by
  constructor
  ¬∑ intro h
    induction n generalizing f with
    | zero => simpa [Digits.ext_iff] using h
    | succ n ih =>
      have h' := h
      rw [evalAtUpto_add_one, add_eq_zero_iff_eq_neg, ‚Üê neg_mul, mul_comm] at h'
      replace h' : œñ ^ (n + 1) ‚à£ evalAtUpto œñ f (n + 1) := by
        rw [h']
        exact ‚ü®_, rfl‚ü©
      replace h' : œñ ‚à£ evalAtUpto œñ f (n + 1) := dvd_trans (dvd_pow_self _ (by simp)) h'
      rw [dvd_evalAtUpto_iff_apply_zero hœñ, Digits.ext_iff, Digits.coe_zero] at h'
      rw [‚Üê sub_zero (evalAtUpto _ _ _), ‚Üê h', ‚Üê mul_evalAtUpto_of_add_one, h'] at h
      simp only [mul_eq_zero, hn, false_or] at h
      specialize ih h
      simp only [Function.comp_apply, Digits.ext_iff, Digits.coe_zero] at ih
      rintro (_|i) hi
      ¬∑ simp [Digits.ext_iff, h']
      ¬∑ simpa [Digits.ext_iff] using ih i (by linarith)
  ¬∑ intro h
    rw [‚Üê zero_evalAtUpto (D := D) œñ (n + 1)]
    refine congr_of_eqOn œñ ?_
    simpa using h

lemma pow_not_dvd_evalAtUpto [IsDomain R] {œñ : R} (hœñ : ¬¨ IsUnit œñ) (hn : œñ ‚â† 0)
    (f : AdicExpansion D)
    (n : ‚Ñï) (h : ‚àÉ i < n + 1, f i ‚â† 0) :
    ¬¨œñ ^ (n + 1) ‚à£ evalAtUpto œñ f (n + 1) := by
  contrapose! h
  rw [‚Üê evalAtUpto_eq_zero_iff hœñ hn]
  induction n generalizing f with
  | zero =>
    simp only [zero_add, pow_one, evalAtUpto_one] at h
    simpa [Digits.ext_iff] using not_isUnit_of_not_isUnit_dvd hœñ h
  | succ n ih =>
    rw [evalAtUpto_add_one, pow_succ] at h
    obtain ‚ü®c, h‚ü© := h
    rw [mul_comm, eq_comm, ‚Üê sub_eq_iff_eq_add, mul_assoc, ‚Üê mul_sub] at h
    specialize ih _ ‚ü®_, h.symm‚ü©
    simp only [ih, mul_eq_zero, ne_eq, AddLeftCancelMonoid.add_eq_zero, one_ne_zero, and_false,
      not_false_eq_true, pow_eq_zero_iff, hn, false_or, sub_eq_zero] at h
    replace h : f (n + 1) = 0 := by simpa using not_isUnit_of_not_isUnit_dvd hœñ ‚ü®_, h.symm‚ü©
    rw [evalAtUpto_add_one, ih, h, Digits.coe_zero]
    simp [hn]

lemma evalAtUpto_injOn [IsDomain R]
    {œñ : R} (hœñ : ¬¨ IsUnit œñ) (hœñ0 : œñ ‚â† 0) (n : ‚Ñï) (z : D) :
    Set.InjOn (evalAtUpto œñ ¬∑ n) {f : AdicExpansion D | ‚àÄ i ‚â• n, f i = z} := by
  induction n generalizing z with
  | zero =>
    intro f hf g hg _
    simp only [ge_iff_le, zero_le, forall_const, Set.mem_setOf_eq] at hf hg
    ext
    simp [hf, hg]
  | succ n ih =>
    intro f hf g hg h
    simp only [ge_iff_le, Set.mem_setOf_eq] at hf hg h
    apply_fun (¬∑ - (f 0 : R)) at h
    apply_fun (¬∑ - (g 0 : R)) at h
    rw [‚Üê mul_evalAtUpto_of_add_one, sub_sub, add_comm (f 0 : R), ‚Üê sub_sub,
      ‚Üê mul_evalAtUpto_of_add_one, sub_eq_sub_iff_sub_eq_sub, ‚Üê mul_sub] at h
    have h0 : g 0 = f 0 := by simp [‚Üê Digits.not_isUnit_dvd_sub_iff hœñ, ‚Üê h]
    simp only [h0, sub_self, mul_eq_zero, hœñ0, false_or, sub_eq_zero] at h
    specialize ih z (fun i hi ‚Ü¶ hf (i + 1) (by simpa using hi))
      (fun i hi ‚Ü¶ hg (i + 1) (by simpa using hi)) h
    ext (_|n)
    ¬∑ simp [h0]
    ¬∑ simp [congr_fun ih n]

section appr

/-- The trailing digit in an adic expansion -- the digit when mod-ding by the base. -/
noncomputable
abbrev trailingDigit (x : R) : D :=
  Function.invFun (IsLocalRing.residue R ‚àò (‚Üë)) (IsLocalRing.residue R x)

@[simp]
lemma trailingDigit_zero : (trailingDigit 0 : D) = 0 := by
  apply Subtype.val_injective
  apply D.bij.injOn (Subtype.prop _) (Subtype.prop _)
  change (IsLocalRing.residue R ‚àò Subtype.val) _ = _
  apply Function.invFun_eq
  refine ‚ü®0, ?_‚ü©
  simp

lemma residue_sub_trailingDigit_eq_zero (x : R) :
    IsLocalRing.residue R (x - (trailingDigit x : D)) = 0 := by
  rw [trailingDigit, RingHom.map_sub, sub_eq_zero, eq_comm]
  change (IsLocalRing.residue R ‚àò Subtype.val) _ = _
  apply Function.invFun_eq
  simpa using D.bij.surjOn (Set.mem_univ (IsLocalRing.residue R x))

lemma sub_trailingDigit_mem_maximalIdeal (x : R) :
    x - (trailingDigit x : D) ‚àà IsLocalRing.maximalIdeal R := by
  rw [‚Üê Ideal.Quotient.eq_zero_iff_mem, ‚Üê IsLocalRing.residue]
  exact residue_sub_trailingDigit_eq_zero x

variable [IsDiscreteValuationRing ùí™[K]] (D : Digits ùí™[K]) {œñ : ùí™[K]} (hœñ : Irreducible œñ)
include hœñ

/-- The quotient when dividing through by the base after subtracting out the trailing digit.
In an adic expansion, this corresponds to the sequence of digits that come after the trailing digit.
-/
noncomputable
abbrev tail (x : ùí™[K]) : ùí™[K] :=
  ‚ü®(x - (trailingDigit x : D)) / œñ, by
    suffices œñ ‚à£ x - (trailingDigit x : D) by
      obtain ‚ü®c, hc‚ü© := this
      simp only [‚Üê AddSubgroupClass.coe_sub, hc, Subring.coe_mul]
      rw [mul_div_cancel_left‚ÇÄ]
      ¬∑ simp
      ¬∑ simp [hœñ.ne_zero]
    rw [‚Üê Ideal.Quotient.eq_zero_iff_dvd, ‚Üê hœñ.maximalIdeal_eq, ‚Üê IsLocalRing.residue]
    exact residue_sub_trailingDigit_eq_zero x‚ü©

@[simp]
lemma tail_zero : tail D hœñ 0 = 0 := by
  ext
  simp [tail]

lemma trailingDigit_mul (x : ùí™[K]) :
    trailingDigit (œñ * x) = (0 : D) := by
  rw [trailingDigit, RingHom.map_mul]
  have : IsLocalRing.residue ùí™[K] œñ = 0 := by
    simp [hœñ.not_unit]
  simp only [this, zero_mul]
  rw [‚Üê (IsLocalRing.residue ùí™[K]).map_zero, ‚Üê trailingDigit]
  simp

@[simp]
lemma tail_mul (x : ùí™[K]) :
    tail D hœñ (œñ * x) = x := by
  ext
  simp only [Subring.coe_mul, trailingDigit_mul D hœñ, Digits.coe_zero, ZeroMemClass.coe_zero,
    sub_zero]
  rw [mul_div_cancel_left‚ÇÄ]
  simp [hœñ.ne_zero]

lemma mul_tail (x : ùí™[K]) :
    œñ * tail D hœñ x = x - (trailingDigit x : D) := by
  ext
  simp only [tail, Subring.coe_mul, AddSubgroupClass.coe_sub]
  rw [mul_div_cancel‚ÇÄ]
  simp [hœñ.ne_zero]

/-- Recursively construct a partial adic expansion at a base, up to `n` digits. This construction
uses an explicit fuel of target of `n` digits to ensure it terminates. -/
noncomputable
def apprUpto : ‚Ñï ‚Üí ùí™[K] ‚Üí AdicExpansion D
  | 0, _ => 0
  | n + 1, x =>
    let d : D := trailingDigit x
    Function.update (apprUpto n (tail D hœñ x) ‚àò (¬∑ - 1)) 0 d

@[simp]
lemma apprUpto_at_zero (x : ùí™[K]) :
    apprUpto D hœñ 0 x = 0 := by
  simp [apprUpto]

lemma apprUpto_eval_zero_eq_invFun (n : ‚Ñï) (x : ùí™[K]) :
    apprUpto D hœñ (n + 1) x 0 = trailingDigit x := by
  simp [apprUpto]

@[simp]
lemma apprUpto_zero (n : ‚Ñï) :
    apprUpto D hœñ n 0 = 0 := by
  induction n with
  | zero => simp
  | succ n ih =>
    simp only [apprUpto]
    ext
    simp [ih]

lemma residue_apprUpto_eval_zero (n : ‚Ñï) (x : ùí™[K]) :
    IsLocalRing.residue ùí™[K] (apprUpto D hœñ (n + 1) x 0) = IsLocalRing.residue ùí™[K] x := by
  rw [apprUpto]
  change (IsLocalRing.residue ùí™[K] ‚àò Subtype.val) _ = _
  apply Function.invFun_eq
  simpa using D.bij.surjOn (Set.mem_univ (IsLocalRing.residue ùí™[K] x))

lemma pow_dvd_sub_evalAtUpto_apprUpto (n : ‚Ñï) (x : ùí™[K]) :
    œñ ^ n ‚à£ x - evalAtUpto œñ (apprUpto D hœñ n x) n := by
  rcases n with (_|n)
  ¬∑ simp
  induction n generalizing x with
  | zero =>
    simp only [zero_add, pow_one, evalAtUpto_one]
    rw [‚Üê Ideal.Quotient.eq_zero_iff_dvd, ‚Üê hœñ.maximalIdeal_eq, RingHom.map_sub, sub_eq_zero,
      eq_comm]
    exact residue_apprUpto_eval_zero D _ _ _
  | succ n ih =>
    rw [apprUpto, evalAtUpto_add_one', Function.update_self, ‚Üê sub_sub, ‚Üê mul_tail D hœñ,
      ‚Üê mul_sub, pow_succ']
    exact mul_dvd_mul_left œñ (ih (tail D hœñ x))

end appr

end AdicExpansion
