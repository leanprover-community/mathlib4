/-
Copyright (c) 2025 Salvatore Mercuri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Salvatore Mercuri
-/
import Mathlib.Algebra.Group.Int.TypeTags
import Mathlib.Algebra.Order.Group.TypeTags
import Mathlib.Algebra.Order.GroupWithZero.Canonical
import Mathlib.Topology.Algebra.Valued.ValuationTopology
import Mathlib.Topology.Algebra.Valued.LocallyCompact
import Mathlib.RingTheory.Ideal.Quotient.Basic

/-! # Topological results for integer-valued rings

This file contains topological results for valuation rings taking values in the
multiplicative integers with zero adjoined. These are useful for cases where there
is a `Valued K â„¤â‚˜â‚€` instance but no canonical base with which to embed this into
`NNReal`.
-/

open Multiplicative WithZero

open scoped Multiplicative Topology

namespace Valued.WithZeroMulInt

open Set Filter in
/-- In a `â„¤â‚˜â‚€`-valued ring, powers of `x` tend to zero if `v x â‰¤ ofAdd (-1 : â„¤)`. -/
theorem tendsto_zero_pow_of_le_neg_one {K : Type*} [Ring K] [Valued K â„¤â‚˜â‚€]
    {x : K} (hx : v x â‰¤ ofAdd (-1 : â„¤)) :
    Tendsto (fun (n : â„•) => x ^ n) atTop (ğ“ 0) := by
  simp only [(hasBasis_nhds_zero _ _).tendsto_right_iff, mem_setOf_eq, map_pow, eventually_atTop]
  have h_lt : ofAdd (-1 : â„¤) < (1 : â„¤â‚˜â‚€) := by
    rw [â† coe_one, coe_lt_coe, â† ofAdd_zero, ofAdd_lt]; linarith
  intro Î³ _
  by_cases hÎ³ : Î³.val â‰¤ 1
  Â· let m := - toAdd (unitsWithZeroEquiv Î³) + 1 |>.toNat
    refine âŸ¨m, fun b hb => lt_of_le_of_lt
      (pow_le_pow_of_le_one zero_le' (le_trans hx <| le_of_lt h_lt) hb) ?_âŸ©
    replace hÎ³ : 0 â‰¤ -toAdd (unitsWithZeroEquiv Î³) + 1 := by
      rw [â† coe_unitsWithZeroEquiv_eq_units_val, â† coe_one, coe_le_coe, â† toAdd_le, toAdd_one] at hÎ³
      linarith
    apply lt_of_le_of_lt <| pow_le_pow_leftâ‚€ zero_le' hx m
    rw [â† coe_unitsWithZeroEquiv_eq_units_val, â† coe_pow, coe_lt_coe, â† ofAdd_nsmul,
      nsmul_eq_mul, Int.toNat_of_nonneg hÎ³]
    simp
    rw [â† ofAdd_zero, ofAdd_lt]
    exact zero_lt_one
  Â· refine âŸ¨1, fun b hb => lt_of_le_of_lt
      (pow_le_pow_of_le_one zero_le' (le_trans hx <| le_of_lt h_lt) hb) ?_âŸ©
    apply lt_trans _ (lt_of_not_le hÎ³)
    apply lt_of_le_of_lt (pow_one (v x) â–¸ hx)
    exact h_lt

open Filter in
theorem exists_pow_lt_of_le_neg_one {K : Type*} [Ring K] [Valued K â„¤â‚˜â‚€]
    {x : K} (hx : v x â‰¤ ofAdd (-1 : â„¤)) (Î³ : â„¤â‚˜â‚€Ë£) :
    âˆƒ n, v x ^ n < Î³ := by
  simp_rw [â† map_pow]
  let âŸ¨n, hnâŸ© := eventually_atTop.1 <|
     ((hasBasis_nhds_zero _ _).tendsto_right_iff ).1 (tendsto_zero_pow_of_le_neg_one hx) Î³ trivial
  use n
  convert Set.mem_setOf_eq â–¸ hn n le_rfl

variable {K : Type*} [Field K] [Valued K â„¤â‚˜â‚€]

theorem irreducible_valuation_lt_one [IsDiscreteValuationRing ğ’ª[K]] {Ï– : ğ’ª[K]}
    (h : Irreducible Ï–) :
    v Ï–.1 < 1 := by
  have := mt (Valuation.integer.integers _).isUnit_iff_valuation_eq_one.2 h.not_unit
  exact lt_of_le_of_ne (Valuation.mem_integer_iff _ _ |>.1 Ï–.2) this

theorem irreducible_valuation_le_ofAdd_neg_one [IsDiscreteValuationRing ğ’ª[K]] {Ï– : ğ’ª[K]}
    (h : Irreducible Ï–) :
    v Ï–.1 â‰¤ ofAdd (-1 : â„¤) := by
  letI := (lt_ofAdd_iff (show v Ï–.1 â‰  0 by simp [h.ne_zero])).1 (irreducible_valuation_lt_one h)
  rw [le_ofAdd_iff (show v Ï–.1 â‰  0 by simp [h.ne_zero])]
  omega

theorem mem_maximalIdeal_pow_valuation [IsDiscreteValuationRing ğ’ª[K]]
    {x : ğ’ª[K]} {n : â„•} (hx : x âˆˆ ğ“‚[K] ^ n) {Ï– : ğ’ª[K]} (h : Irreducible Ï–) :
    v x.val â‰¤ v Ï–.1 ^ n := by
  by_cases hxâ‚€ : x = 0
  Â· simp [hxâ‚€]
  Â· simp_rw [h.maximalIdeal_eq, Ideal.span_singleton_pow, Ideal.mem_span_singleton] at hx
    let âŸ¨y, hyâŸ© := hx
    simp [hy]
    exact le_trans (mul_le_of_le_one_right' <| (Valuation.mem_integer_iff _ _).1 y.2) le_rfl

theorem units_toAdd_le_of_le {Î± : Type*} [AddGroup Î±] [Preorder Î±]
    {Î³ : (WithZero (Multiplicative Î±))Ë£} {m : (WithZero (Multiplicative Î±))} (hm : m â‰  0)
    (hÎ³ : Î³.val â‰¤ m) :
    toAdd (unitsWithZeroEquiv Î³) â‰¤ toAdd (m.unzero hm) := by
  rw [â† ofAdd_le, ofAdd_toAdd, â† coe_le_coe, unitsWithZeroEquiv, MulEquiv.coe_mk,
    Equiv.coe_fn_mk, coe_unzero]
  apply le_trans hÎ³
  rw [ofAdd_toAdd, coe_unzero]

/-- The ring of integers `ğ’ª[K]` of a `â„¤â‚˜â‚€`-valued field `K` with finite residue
field has a finite covering by elements of the basis of uniformity of `K`, whenever
`ğ’ª[K]` is a discrete valuation ring. -/
theorem finite_cover_of_uniformity_basis [IsDiscreteValuationRing ğ’ª[K]] {Î³ : â„¤â‚˜â‚€Ë£}
    (h : Finite ğ“€[K]) :
    âˆƒ t : Set K, Set.Finite t âˆ§
      (ğ’ª[K]).carrier âŠ† â‹ƒ y âˆˆ t, { x | (x, y) âˆˆ { p | v (p.2 - p.1) < Î³.val } } := by
  classical
  let âŸ¨Ï–, hÏ–âŸ© := IsDiscreteValuationRing.exists_irreducible ğ’ª[K]
  let âŸ¨m, hmâŸ© := exists_pow_lt_of_le_neg_one (irreducible_valuation_le_ofAdd_neg_one hÏ–) Î³
  letI := integer.finite_quotient_maximalIdeal_pow_of_finite_residueField h m
  have h := Fintype.ofFinite (ğ’ª[K] â§¸ ğ“‚[K] ^ m)
  let T := Subtype.val '' (h.elems.image Quotient.out).toSet
  refine âŸ¨T, (Set.Finite.image _ (Finset.finite_toSet _)), fun x hx => ?_âŸ©
  simp only [Set.mem_iUnion]
  let y := (Ideal.Quotient.mk (ğ“‚[K] ^ m) âŸ¨x, hxâŸ©).out
  refine âŸ¨y, Set.mem_image_of_mem _ <| Finset.mem_image_of_mem _ (h.complete _),
    lt_of_le_of_lt (mem_maximalIdeal_pow_valuation (Ideal.Quotient.out_sub _ _) hÏ–) hmâŸ©

variable (K)

/-- The ring of integers `ğ’ª[K]` of a complete `â„¤â‚˜â‚€`-valued field `K` with finite residue
field is compact, whenever `ğ’ª[K]` is a discrete valuation ring. -/
theorem integer_compactSpace [CompleteSpace K] [IsDiscreteValuationRing ğ’ª[K]] (h : Finite ğ“€[K]) :
    CompactSpace ğ’ª[K] := by
  refine CompactSpace.mk (isCompact_iff_isCompact_univ.1 <| ?_)
  exact isCompact_iff_totallyBounded_isComplete.2
    âŸ¨(hasBasis_uniformity _ _).totallyBounded_iff.2 <| fun _ hÎ³ =>
      finite_cover_of_uniformity_basis h, (integer_isClosed K).isCompleteâŸ©

end Valued.WithZeroMulInt
