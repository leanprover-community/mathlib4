/-
Copyright (c) 2025 Salvatore Mercuri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Salvatore Mercuri
-/
import Mathlib.Topology.Algebra.Valued.LocallyCompact

/-!
# Topological results for integer-valued rings

This file contains topological results for valuation rings taking values in the
multiplicative integers with zero adjoined. These are useful for cases where there
is a `Valued R â„¤â‚˜â‚€` instance but no canonical base with which to embed this into
`NNReal`.
-/

open Filter WithZero Set Multiplicative
open scoped Topology

namespace Valued
variable {R Î“â‚€ : Type*} [Ring R] [LinearOrderedCommGroupWithZero Î“â‚€]

-- TODO: use ValuativeRel after #26833
lemma tendsto_zero_pow_of_v_lt_one [MulArchimedean Î“â‚€] [Valued R Î“â‚€] {x : R} (hx : v x < 1) :
    Tendsto (fun n : â„• â†¦ x ^ n) atTop (ð“ 0) := by
  simp only [(hasBasis_nhds_zero _ _).tendsto_right_iff, mem_setOf_eq, map_pow, eventually_atTop,
    forall_const]
  intro y
  obtain âŸ¨n, hnâŸ© := exists_pow_ltâ‚€ hx y
  refine âŸ¨n, fun m hm â†¦ ?_âŸ©
  refine hn.trans_le' ?_
  exact pow_le_pow_right_of_le_one' hx.le hm

/-- In a `â„¤áµâ°`-valued ring, powers of `x` tend to zero if `v x â‰¤ exp (-1)`. -/
lemma tendsto_zero_pow_of_le_exp_neg_one [Valued R â„¤áµâ°] {x : R} (hx : v x â‰¤ exp (-1)) :
    Tendsto (fun n : â„• â†¦ x ^ n) atTop (ð“ 0) := by
  refine tendsto_zero_pow_of_v_lt_one (hx.trans_lt ?_)
  rw [â† exp_zero, exp_lt_exp]
  norm_num

lemma exists_pow_lt_of_le_exp_neg_one [Valued R â„¤áµâ°] {x : R} (hx : v x â‰¤ exp (-1)) (Î³ : â„¤áµâ°Ë£) :
    âˆƒ n, v x ^ n < Î³ := by
  refine exists_pow_ltâ‚€ (hx.trans_lt ?_) _
  rw [â† exp_zero, exp_lt_exp]
  norm_num

variable {K : Type*} [Field K] [Valued K â„¤áµâ°]

theorem irreducible_valuation_lt_one {Ï– : ð’ª[K]} (h : Irreducible Ï–) : v Ï–.1 < 1 :=
  lt_of_le_of_ne (Valuation.mem_integer_iff _ _ |>.1 Ï–.2) <|
    mt (Valuation.integer.integers _).isUnit_iff_valuation_eq_one.2 h.not_isUnit

theorem irreducible_valuation_le_ofAdd_neg_one {Ï– : ð’ª[K]} (h : Irreducible Ï–) :
    v Ï–.1 â‰¤ ofAdd (-1 : â„¤) := by
  have := (lt_ofAdd_iff (show v Ï–.1 â‰  0 by simp [h.ne_zero])).1 (irreducible_valuation_lt_one h)
  rw [le_ofAdd_iff (show v Ï–.1 â‰  0 by simp [h.ne_zero])]
  omega

theorem mem_maximalIdeal_pow_valuation [IsDiscreteValuationRing ð’ª[K]]
    {x : ð’ª[K]} {n : â„•} (hx : x âˆˆ ð“‚[K] ^ n) {Ï– : ð’ª[K]} (h : Irreducible Ï–) :
    v x.val â‰¤ v Ï–.1 ^ n := by
  rcases eq_or_ne x 0 with (rfl | hxâ‚€) <;> try simp
  simp_rw [h.maximalIdeal_eq, Ideal.span_singleton_pow, Ideal.mem_span_singleton] at hx
  obtain âŸ¨y, rflâŸ© := hx
  simpa using le_trans (mul_le_of_le_one_right' <| (Valuation.mem_integer_iff _ _).1 y.2) le_rfl

/-- The ring of integers `ð’ª[K]` of a `â„¤â‚˜â‚€`-valued field `K` with finite residue
field has a finite covering by elements of the basis of uniformity of `K`, whenever
`ð’ª[K]` is a discrete valuation ring. -/
theorem finite_cover_of_uniformity_basis [IsDiscreteValuationRing ð’ª[K]] {Î³ : â„¤áµâ°Ë£}
    (h : Finite ð“€[K]) :
    âˆƒ t : Set K, Set.Finite t âˆ§
      (ð’ª[K]).carrier âŠ† â‹ƒ y âˆˆ t, { x | (x, y) âˆˆ { p | v (p.2 - p.1) < Î³.val } } := by
  classical
  let âŸ¨Ï–, hÏ–âŸ© := IsDiscreteValuationRing.exists_irreducible ð’ª[K]
  let âŸ¨m, hmâŸ© := exists_pow_lt_of_le_exp_neg_one (irreducible_valuation_le_ofAdd_neg_one hÏ–) Î³
  have := integer.finite_quotient_maximalIdeal_pow_of_finite_residueField h m
  have h := Fintype.ofFinite (ð’ª[K] â§¸ ð“‚[K] ^ m)
  let T := Subtype.val '' (h.elems.image Quotient.out).toSet
  refine âŸ¨T, (Set.Finite.image _ (Finset.finite_toSet _)), fun x hx â†¦ ?_âŸ©
  simp only [Set.mem_iUnion]
  let y := (Ideal.Quotient.mk (ð“‚[K] ^ m) âŸ¨x, hxâŸ©).out
  exact âŸ¨y, Set.mem_image_of_mem _ <| Finset.mem_image_of_mem _ (h.complete _),
    lt_of_le_of_lt (mem_maximalIdeal_pow_valuation (Ideal.Quotient.out_sub _ _) hÏ–) hmâŸ©

variable (K)

/-- The ring of integers `ð’ª[K]` of a complete `â„¤â‚˜â‚€`-valued field `K` with finite residue
field is compact, whenever `ð’ª[K]` is a discrete valuation ring. -/
theorem integer_compactSpace [CompleteSpace K] [IsDiscreteValuationRing ð’ª[K]] (h : Finite ð“€[K]) :
    CompactSpace ð’ª[K] where
   isCompact_univ := isCompact_iff_isCompact_univ.1 <| isCompact_iff_totallyBounded_isComplete.2
      âŸ¨(hasBasis_uniformity _ _).totallyBounded_iff.2 fun _ _ â†¦ finite_cover_of_uniformity_basis h,
        (integer_isClosed K).isCompleteâŸ©

end Valued
