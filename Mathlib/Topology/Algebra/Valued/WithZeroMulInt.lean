/-
Copyright (c) 2025 Salvatore Mercuri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Salvatore Mercuri
-/
import Mathlib.Algebra.Order.Group.TypeTags
import Mathlib.Topology.Algebra.Valued.ValuationTopology
import Mathlib.Topology.Algebra.Valued.LocallyCompact
import Mathlib.RingTheory.Ideal.Quotient.Basic

/-! # Topological results for integer-valued rings

This file contains topological results for valuation rings taking values in the
multiplicative integers with zero adjoined. These are useful for cases where there
is a `Valued K â„¤â‚˜â‚€` instance but no canonical base with which to embed this into
`NNReal` and use topological results on `NNReal`.
-/

open Multiplicative WithZero

open scoped Multiplicative Topology

namespace Valued.WithZeroMulInt

open Filter in
/-- In a `â„¤â‚˜â‚€`-valued ring, powers of `x` tend to zero if `v x â‰¤ -1`. -/
theorem tendsto_zero_pow_of_le_neg_one {K : Type*} [Ring K] [Valued K â„¤â‚˜â‚€]
    {x : K} (hx : Valued.v x â‰¤ ofAdd (-1 : â„¤)) :
    Tendsto (fun (n : â„•) => x ^ n) atTop (ğ“ 0) := by
  simp only [(Valued.hasBasis_nhds_zero _ _).tendsto_right_iff, Set.mem_setOf_eq, map_pow,
    eventually_atTop]
  have h_lt : ofAdd (-1 : â„¤) < (1 : â„¤â‚˜â‚€) := by
    rw [â† coe_one, coe_lt_coe, â† ofAdd_zero, ofAdd_lt]; linarith
  intro Î³ _
  by_cases hÎ³ : Î³.val â‰¤ 1
  Â· let m := - toAdd (unitsWithZeroEquiv Î³) + 1 |>.toNat
    refine âŸ¨m, fun b hb => lt_of_le_of_lt
      (pow_le_pow_of_le_one zero_le' (le_trans hx <| le_of_lt h_lt) hb) ?_âŸ©
    replace hÎ³ : 0 â‰¤ -toAdd (unitsWithZeroEquiv Î³) + 1 := by
      rw [â† coe_unitsWithZeroEquiv_eq_units_val, â† coe_one, coe_le_coe, â† toAdd_le, toAdd_one] at hÎ³
      linarith
    apply lt_of_le_of_lt <| pow_le_pow_leftâ‚€ zero_le' hx m
    rw [â† coe_unitsWithZeroEquiv_eq_units_val, â† coe_pow, coe_lt_coe, â† ofAdd_nsmul,
      nsmul_eq_mul, Int.toNat_of_nonneg hÎ³]
    simp
    rw [â† ofAdd_zero, ofAdd_lt]
    exact zero_lt_one
  Â· refine âŸ¨1, fun b hb => lt_of_le_of_lt
      (pow_le_pow_of_le_one zero_le' (le_trans hx <| le_of_lt h_lt) hb) ?_âŸ©
    apply lt_trans _ (lt_of_not_le hÎ³)
    apply lt_of_le_of_lt (pow_one (Valued.v x) â–¸ hx)
    exact h_lt

variable (K : Type*) [Field K] [Valued K â„¤â‚˜â‚€]

theorem hasBasis_uniformity_le_one [IsDiscreteValuationRing ğ’ª[K]] :
    (uniformity K).HasBasis
      (fun (Î³ : â„¤â‚˜â‚€Ë£) => Î³ â‰¤ 1) (fun (Î³ : â„¤â‚˜â‚€Ë£) => { p | Valued.v (p.2 - p.1) < Î³ }) := by
  have hq (Î³ : â„¤â‚˜â‚€Ë£) (_ : True) :
    âˆƒ (Î³' : â„¤â‚˜â‚€Ë£), True âˆ§ Î³' â‰¤ 1 âˆ§
      { p : K Ã— _ | Valued.v (p.2 - p.1) < Î³' } âŠ†
        { p | Valued.v (p.2 - p.1) < Î³ } := by
    choose Ï– _ using IsDiscreteValuationRing.exists_irreducible ğ’ª[K]
    by_cases hÎ³ : 1 < Î³
    Â· exact âŸ¨1, trivial, le_refl _, fun _ hx => lt_trans (Set.mem_setOf.1 hx) hÎ³âŸ©
    Â· exact âŸ¨Î³, trivial, not_lt.1 hÎ³, subset_rflâŸ©
  convert (Valued.hasBasis_uniformity _ _).restrict hq
  simp only [true_and]

variable {K}

theorem valuation_le_pow_of_maximalIdeal [IsDiscreteValuationRing ğ’ª[K]]
    {x : ğ’ª[K]} (n : â„•) (hx : x âˆˆ ğ“‚[K] ^ n) :
    Valued.v x.val â‰¤ Multiplicative.ofAdd (-n : â„¤) := by
  sorry

theorem units_toAdd_le_of_le {Î± : Type*} [AddGroup Î±] [Preorder Î±]
    {Î³ : (WithZero (Multiplicative Î±))Ë£} {m : (WithZero (Multiplicative Î±))} (hm : m â‰  0)
    (hÎ³ : Î³.val â‰¤ m) :
    toAdd (unitsWithZeroEquiv Î³) â‰¤ toAdd (m.unzero hm) := by
  rw [â† ofAdd_le, ofAdd_toAdd, â† coe_le_coe, unitsWithZeroEquiv, MulEquiv.coe_mk,
    Equiv.coe_fn_mk, coe_unzero]
  apply le_trans hÎ³
  rw [ofAdd_toAdd, coe_unzero]

open scoped Classical in
/-- There is a finite covering of the `v`-adic integers of open balls of radius less than one,
obtained by using the finite representatives in the quotient of the `v`-adic integers by an
appropriate power of the maximal ideal. -/
theorem finite_subcover_of_uniformity_basis [IsDiscreteValuationRing ğ’ª[K]] {Î³ : â„¤â‚˜â‚€Ë£}
    (h : Finite ğ“€[K]) (hÎ³ : Î³.val â‰¤ 1) :
    âˆƒ t : Set K, Set.Finite t âˆ§
      (ğ’ª[K]).carrier âŠ† â‹ƒ y âˆˆ t,
        { x | (x, y) âˆˆ { p | Valued.v (p.2 - p.1) < Î³.val } } := by
  let m := (- toAdd (unitsWithZeroEquiv Î³) + 1).toNat
  letI := integer.finite_quotient_maximalIdeal_pow_of_finite_residueField h m
  have h := Fintype.ofFinite (ğ’ª[K] â§¸ ğ“‚[K] ^ m)
  let T := h.elems.image Quotient.out
  refine âŸ¨Subtype.val '' T.toSet, (Set.Finite.image _ (Finset.finite_toSet _)), fun x hx => ?_âŸ©
  simp only [Set.mem_iUnion]
  let y := (Ideal.Quotient.mk (ğ“‚[K] ^ m) âŸ¨x, hxâŸ©).out
  refine âŸ¨y, Set.mem_image_of_mem _ <| Finset.mem_image_of_mem _ (h.complete _),
    lt_of_le_of_lt (valuation_le_pow_of_maximalIdeal _ (Ideal.Quotient.out_sub _ _)) ?_âŸ©
  simp only [m, â† coe_unitsWithZeroEquiv_eq_units_val, coe_lt_coe]
  rw [â† ofAdd_toAdd (unitsWithZeroEquiv Î³), ofAdd_lt, ofAdd_toAdd, Int.toNat_of_nonneg]
  Â· linarith
  Â· simp only [le_neg_add_iff_add_le, add_zero]
    exact le_trans (units_toAdd_le_of_le one_ne_zero hÎ³) zero_le_one

variable (K)

open Set Valued in
theorem integers_isClosed : IsClosed (ğ’ª[K] : Set K) := by
  refine isClosed_iff_nhds.2 fun x hx => ?_
  simp only [isClosed_iff_nhds, SetLike.mem_coe, Valuation.mem_integer_iff, not_le] at hx âŠ¢
  contrapose! hx
  refine âŸ¨{y | Valued.v y = Valued.v x}, loc_const (ne_zero_of_lt hx),
    subset_empty_iff.1 fun y âŸ¨hyâ‚, hyâ‚‚âŸ© => ?_âŸ©
  exact (not_lt_of_le <| hyâ‚‚) <| hyâ‚.symm â–¸ hx

theorem integers_compactSpace {K : Type*} [Field K] [Valued K â„¤â‚˜â‚€] [CompleteSpace K]
    [IsDiscreteValuationRing ğ’ª[K]] (h : Finite ğ“€[K]) : CompactSpace ğ’ª[K] := by
  refine CompactSpace.mk (isCompact_iff_isCompact_univ.1 <| ?_)
  exact isCompact_iff_totallyBounded_isComplete.2
    âŸ¨(hasBasis_uniformity_le_one K).totallyBounded_iff.2 <| fun _ hÎ³ =>
      finite_subcover_of_uniformity_basis h hÎ³, (integers_isClosed K).isCompleteâŸ©

end Valued.WithZeroMulInt
