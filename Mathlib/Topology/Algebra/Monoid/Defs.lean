/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl
-/
import Mathlib.Topology.Constructions.SumProd

/-!
# Topological monoids - definitions

In this file we define two mixin typeclasses:

- `ContinuousMul M` says that the multiplication on `M` is continuous as a function on `M √ó M`;
- `ContinuousAdd M` says that the addition on `M` is continuous as a function on `M √ó M`.

These classes are `Prop`-valued mixins,
i.e., they take data (`TopologicalSpace`, `Mul`/`Add`) as arguments
instead of extending typeclasses with these fields.

We also provide convenience dot notation lemmas like `Filter.Tendsto.mul` and `ContinuousAt.add`.
-/

open scoped Topology

/-- Basic hypothesis to talk about a topological additive monoid or a topological additive
semigroup. A topological additive monoid over `M`, for example, is obtained by requiring both the
instances `AddMonoid M` and `ContinuousAdd M`.

Continuity in only the left/right argument can be stated using
`ContinuousConstVAdd Œ± Œ±`/`ContinuousConstVAdd Œ±·µê·µí·µñ Œ±`. -/
class ContinuousAdd (M : Type*) [TopologicalSpace M] [Add M] : Prop where
  continuous_add : Continuous fun p : M √ó M ‚Ü¶ p.1 + p.2

/-- Basic hypothesis to talk about a topological monoid or a topological semigroup.
A topological monoid over `M`, for example, is obtained by requiring both the instances `Monoid M`
and `ContinuousMul M`.

Continuity in only the left/right argument can be stated using
`ContinuousConstSMul Œ± Œ±`/`ContinuousConstSMul Œ±·µê·µí·µñ Œ±`. -/
@[to_additive]
class ContinuousMul (M : Type*) [TopologicalSpace M] [Mul M] : Prop where
  continuous_mul : Continuous fun p : M √ó M ‚Ü¶ p.1 * p.2

section ContinuousMul

variable {M : Type*} [TopologicalSpace M] [Mul M] [ContinuousMul M]

@[to_additive (attr := continuity, fun_prop)]
theorem continuous_mul : Continuous fun p : M √ó M ‚Ü¶ p.1 * p.2 :=
  ContinuousMul.continuous_mul

@[to_additive]
theorem Filter.Tendsto.mul {Œ± : Type*} {f g : Œ± ‚Üí M} {x : Filter Œ±} {a b : M}
    (hf : Tendsto f x (ùìù a)) (hg : Tendsto g x (ùìù b)) : Tendsto (fun x ‚Ü¶ f x * g x) x (ùìù (a * b)) :=
  (continuous_mul.tendsto _).comp (hf.prodMk_nhds hg)

variable {X : Type*} [TopologicalSpace X] {f g : X ‚Üí M} {s : Set X} {x : X}

@[to_additive (attr := continuity, fun_prop)]
theorem Continuous.mul (hf : Continuous f) (hg : Continuous g) :
    Continuous fun x ‚Ü¶ f x * g x :=
  continuous_mul.comp‚ÇÇ hf hg

@[to_additive]
theorem ContinuousWithinAt.mul (hf : ContinuousWithinAt f s x) (hg : ContinuousWithinAt g s x) :
    ContinuousWithinAt (fun x ‚Ü¶ f x * g x) s x :=
  Filter.Tendsto.mul hf hg

@[to_additive (attr := fun_prop)]
theorem ContinuousAt.mul (hf : ContinuousAt f x) (hg : ContinuousAt g x) :
    ContinuousAt (fun x ‚Ü¶ f x * g x) x :=
  Filter.Tendsto.mul hf hg

@[to_additive (attr := fun_prop)]
theorem ContinuousOn.mul (hf : ContinuousOn f s) (hg : ContinuousOn g s) :
    ContinuousOn (fun x ‚Ü¶ f x * g x) s := fun x hx ‚Ü¶
  (hf x hx).mul (hg x hx)

end ContinuousMul
