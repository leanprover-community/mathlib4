/-
Copyright (c) 2021 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
import Mathlib.Algebra.Group.Pointwise.Set.Finite
import Mathlib.Algebra.Order.Group.Pointwise.Interval
import Mathlib.Algebra.Order.Group.Units
import Mathlib.Order.Interval.Finset.Defs
import Mathlib.Topology.Algebra.GroupWithZero
import Mathlib.Topology.Order.OrderClosed

/-!
# The topology on linearly ordered commutative groups with zero

Let `Î“â‚€` be a linearly ordered commutative group to which we have adjoined a zero element.  Then
`Î“â‚€` may naturally be endowed with a topology that turns `Î“â‚€` into a topological monoid.
Neighborhoods of zero are sets containing `{ Î³ | Î³ < Î³â‚€ }` for some invertible element `Î³â‚€` and
every invertible element is open.  In particular the topology is the following: "a subset `U âŠ† Î“â‚€`
is open if `0 âˆ‰ U` or if there is an invertible `Î³â‚€ âˆˆ Î“â‚€` such that `{ Î³ | Î³ < Î³â‚€ } âŠ† U`", see
`WithZeroTopology.isOpen_iff`.

We prove this topology is ordered and Tâ‚… (in addition to be compatible with the monoid
structure).

All this is useful to extend a valuation to a completion. This is an abstract version of how the
absolute value (resp. `p`-adic absolute value) on `â„š` is extended to `â„` (resp. `â„šâ‚š`).

## Implementation notes

This topology is defined as a scoped instance since it may not be the desired topology on
a linearly ordered commutative group with zero. You can locally activate this topology using
`open WithZeroTopology`.
-/

open Topology Filter TopologicalSpace Filter Set Function

namespace WithZeroTopology

variable {Î± Î“â‚€ : Type*} [LinearOrderedCommGroupWithZero Î“â‚€] {Î³ Î³â‚ Î³â‚‚ : Î“â‚€} {l : Filter Î±}
  {f : Î± â†’ Î“â‚€}

/-- The topology on a linearly ordered commutative group with a zero element adjoined.
A subset U is open if 0 âˆ‰ U or if there is an invertible element Î³â‚€ such that {Î³ | Î³ < Î³â‚€} âŠ† U. -/
scoped instance (priority := 100) topologicalSpace : TopologicalSpace Î“â‚€ :=
  nhdsAdjoint 0 <| â¨… Î³ â‰  0, ğ“Ÿ (Iio Î³)

theorem nhds_eq_update : (ğ“ : Î“â‚€ â†’ Filter Î“â‚€) = update pure 0 (â¨… Î³ â‰  0, ğ“Ÿ (Iio Î³)) := by
  rw [nhds_nhdsAdjoint, sup_of_le_right]
  exact le_iInfâ‚‚ fun Î³ hÎ³ â†¦ le_principal_iff.2 <| zero_lt_iff.2 hÎ³

/-!
### Neighbourhoods of zero
-/

theorem nhds_zero : ğ“ (0 : Î“â‚€) = â¨… Î³ â‰  0, ğ“Ÿ (Iio Î³) := by
  rw [nhds_eq_update, update_self]

/-- In a linearly ordered group with zero element adjoined, `U` is a neighbourhood of `0` if and
only if there exists a nonzero element `Î³â‚€` such that `Iio Î³â‚€ âŠ† U`. -/
theorem hasBasis_nhds_zero : (ğ“ (0 : Î“â‚€)).HasBasis (fun Î³ : Î“â‚€ => Î³ â‰  0) Iio := by
  rw [nhds_zero]
  refine hasBasis_biInf_principal ?_ âŸ¨1, one_ne_zeroâŸ©
  exact directedOn_iff_directed.2 (Monotone.directed_ge fun a b hab => Iio_subset_Iio hab)

theorem Iio_mem_nhds_zero (hÎ³ : Î³ â‰  0) : Iio Î³ âˆˆ ğ“ (0 : Î“â‚€) :=
  hasBasis_nhds_zero.mem_of_mem hÎ³

/-- If `Î³` is an invertible element of a linearly ordered group with zero element adjoined, then
`Iio (Î³ : Î“â‚€)` is a neighbourhood of `0`. -/
theorem nhds_zero_of_units (Î³ : Î“â‚€Ë£) : Iio â†‘Î³ âˆˆ ğ“ (0 : Î“â‚€) :=
  Iio_mem_nhds_zero Î³.ne_zero

theorem tendsto_zero : Tendsto f l (ğ“ (0 : Î“â‚€)) â†” âˆ€ (Î³â‚€) (_ : Î³â‚€ â‰  0), âˆ€á¶  x in l, f x < Î³â‚€ := by
  simp [nhds_zero]

/-!
### Neighbourhoods of non-zero elements
-/

/-- The neighbourhood filter of a nonzero element consists of all sets containing that
element. -/
@[simp]
theorem nhds_of_ne_zero {Î³ : Î“â‚€} (hâ‚€ : Î³ â‰  0) : ğ“ Î³ = pure Î³ :=
  nhds_nhdsAdjoint_of_ne _ hâ‚€

/-- The neighbourhood filter of an invertible element consists of all sets containing that
element. -/
theorem nhds_coe_units (Î³ : Î“â‚€Ë£) : ğ“ (Î³ : Î“â‚€) = pure (Î³ : Î“â‚€) :=
  nhds_of_ne_zero Î³.ne_zero

/-- If `Î³` is an invertible element of a linearly ordered group with zero element adjoined, then
`{Î³}` is a neighbourhood of `Î³`. -/
theorem singleton_mem_nhds_of_units (Î³ : Î“â‚€Ë£) : ({â†‘Î³} : Set Î“â‚€) âˆˆ ğ“ (Î³ : Î“â‚€) := by simp

/-- If `Î³` is a nonzero element of a linearly ordered group with zero element adjoined, then `{Î³}`
is a neighbourhood of `Î³`. -/
theorem singleton_mem_nhds_of_ne_zero (h : Î³ â‰  0) : ({Î³} : Set Î“â‚€) âˆˆ ğ“ (Î³ : Î“â‚€) := by simp [h]

theorem hasBasis_nhds_of_ne_zero {x : Î“â‚€} (h : x â‰  0) :
    HasBasis (ğ“ x) (fun _ : Unit => True) fun _ => {x} := by
  rw [nhds_of_ne_zero h]
  exact hasBasis_pure _

theorem hasBasis_nhds_units (Î³ : Î“â‚€Ë£) :
    HasBasis (ğ“ (Î³ : Î“â‚€)) (fun _ : Unit => True) fun _ => {â†‘Î³} :=
  hasBasis_nhds_of_ne_zero Î³.ne_zero

theorem tendsto_of_ne_zero {Î³ : Î“â‚€} (h : Î³ â‰  0) : Tendsto f l (ğ“ Î³) â†” âˆ€á¶  x in l, f x = Î³ := by
  rw [nhds_of_ne_zero h, tendsto_pure]

theorem tendsto_units {Î³â‚€ : Î“â‚€Ë£} : Tendsto f l (ğ“ (Î³â‚€ : Î“â‚€)) â†” âˆ€á¶  x in l, f x = Î³â‚€ :=
  tendsto_of_ne_zero Î³â‚€.ne_zero

theorem Iio_mem_nhds (h : Î³â‚ < Î³â‚‚) : Iio Î³â‚‚ âˆˆ ğ“ Î³â‚ := by
  rcases eq_or_ne Î³â‚ 0 with (rfl | hâ‚€) <;> simp [*, h.ne', Iio_mem_nhds_zero]

/-!
### Open/closed sets
-/

theorem isOpen_iff {s : Set Î“â‚€} : IsOpen s â†” (0 : Î“â‚€) âˆ‰ s âˆ¨ âˆƒ Î³, Î³ â‰  0 âˆ§ Iio Î³ âŠ† s := by
  rw [isOpen_iff_mem_nhds, â† and_forall_ne (0 : Î“â‚€)]
  simp +contextual [nhds_of_ne_zero, imp_iff_not_or,
    hasBasis_nhds_zero.mem_iff]

theorem isClosed_iff {s : Set Î“â‚€} : IsClosed s â†” (0 : Î“â‚€) âˆˆ s âˆ¨ âˆƒ Î³, Î³ â‰  0 âˆ§ s âŠ† Ici Î³ := by
  simp only [â† isOpen_compl_iff, isOpen_iff, mem_compl_iff, not_not, â† compl_Ici,
    compl_subset_compl]

theorem isOpen_Iio {a : Î“â‚€} : IsOpen (Iio a) :=
  isOpen_iff.mpr <| imp_iff_not_or.mp fun ha => âŸ¨a, ne_of_gt ha, Subset.rflâŸ©

lemma isOpen_singleton (h : Î³ â‰  0) : IsOpen {Î³} := by simp [isOpen_iff, h.symm]

/-!
### Instances
-/

/-- The topology on a linearly ordered group with zero element adjoined is compatible with the order
structure: the set `{p : Î“â‚€ Ã— Î“â‚€ | p.1 â‰¤ p.2}` is closed. -/
@[nolint defLemma]
scoped instance (priority := 100) orderClosedTopology : OrderClosedTopology Î“â‚€ where
  isClosed_le' := by
    simp only [â† isOpen_compl_iff, compl_setOf, not_le, isOpen_iff_mem_nhds]
    rintro âŸ¨a, bâŸ© (hab : b < a)
    rw [nhds_prod_eq, nhds_of_ne_zero (zero_le'.trans_lt hab).ne', pure_prod]
    exact Iio_mem_nhds hab

/-- The topology on a linearly ordered group with zero element adjoined is Tâ‚…. -/
@[nolint defLemma]
scoped instance (priority := 100) t5Space : T5Space Î“â‚€ where
  completely_normal := fun s t hâ‚ hâ‚‚ => by
    by_cases hs : 0 âˆˆ s
    Â· have ht : 0 âˆ‰ t := fun ht => disjoint_left.1 hâ‚ (subset_closure hs) ht
      rwa [(isOpen_iff.2 (.inl ht)).nhdsSet_eq, disjoint_nhdsSet_principal]
    Â· rwa [(isOpen_iff.2 (.inl hs)).nhdsSet_eq, disjoint_principal_nhdsSet]

/-- The topology on a linearly ordered group with zero element adjoined makes it a topological
monoid. -/
@[nolint defLemma]
scoped instance (priority := 100) : ContinuousMul Î“â‚€ where
  continuous_mul := by
    simp only [continuous_iff_continuousAt, ContinuousAt]
    rintro âŸ¨x, yâŸ©
    wlog hle : x â‰¤ y generalizing x y
    Â· have := (this y x (le_of_not_ge hle)).comp (continuous_swap.tendsto (x, y))
      simpa only [mul_comm, Function.comp_def, Prod.swap] using this
    rcases eq_or_ne x 0 with (rfl | hx) <;> [rcases eq_or_ne y 0 with (rfl | hy); skip]
    Â· rw [zero_mul]
      refine ((hasBasis_nhds_zero.prod_nhds hasBasis_nhds_zero).tendsto_iff hasBasis_nhds_zero).2
        fun Î³ hÎ³ => âŸ¨(Î³, 1), âŸ¨hÎ³, one_ne_zeroâŸ©, ?_âŸ©
      rintro âŸ¨x, yâŸ© âŸ¨hx : x < Î³, hy : y < 1âŸ©
      exact (mul_lt_mul'' hx hy zero_le' zero_le').trans_eq (mul_one Î³)
    Â· rw [zero_mul, nhds_prod_eq, nhds_of_ne_zero hy, prod_pure, tendsto_map'_iff]
      refine (hasBasis_nhds_zero.tendsto_iff hasBasis_nhds_zero).2 fun Î³ hÎ³ => ?_
      refine âŸ¨Î³ / y, div_ne_zero hÎ³ hy, fun x hx => ?_âŸ©
      calc x * y < Î³ / y * y := mul_lt_mul_of_pos_right hx (zero_lt_iff.2 hy)
      _ = Î³ := div_mul_cancelâ‚€ _ hy
    Â· have hy : y â‰  0 := ((zero_lt_iff.mpr hx).trans_le hle).ne'
      rw [nhds_prod_eq, nhds_of_ne_zero hx, nhds_of_ne_zero hy, prod_pure_pure]
      exact pure_le_nhds (x * y)

@[nolint defLemma]
scoped instance (priority := 100) : HasContinuousInvâ‚€ Î“â‚€ :=
  âŸ¨fun Î³ h => by
    rw [ContinuousAt, nhds_of_ne_zero h]
    exact pure_le_nhds Î³â»Â¹âŸ©

scoped instance : DiscreteTopology Î“â‚€Ë£ := by
  simp [discreteTopology_iff_singleton_mem_nhds, nhds_induced, nhds_prod_eq, Units.val_inj]

lemma isOpenEmbedding_units_val : IsOpenEmbedding (Units.val : Î“â‚€Ë£ â†’ Î“â‚€) where
  eq_induced := by
    simp [â† isInducing_iff, isInducing_iff_nhds, â† Set.image_singleton,
      Units.val_injective.preimage_image]
  injective := Units.val_injective
  isOpen_range := by simp [isOpen_iff]

lemma locallyCompactSpace_of_compact_Iic {x : Î“â‚€} (hx : x â‰  0)
    (h : IsCompact (Iic x)) : LocallyCompactSpace Î“â‚€ where
  local_compact_nhds y s hy := by
    rcases (GroupWithZero.eq_zero_or_unit y).symm with âŸ¨y, rflâŸ© | rfl
    Â· use {y.val}
      simpa using hy
    Â· simp only [hasBasis_nhds_zero.mem_iff, ne_eq] at hy âŠ¢
      obtain âŸ¨r, hr', hrâŸ© := hy
      lift x to Î“â‚€Ë£ using IsUnit.mk0 _ hx
      lift r to Î“â‚€Ë£ using IsUnit.mk0 _ hr'
      rcases subsingleton_or_nontrivial Î“â‚€Ë£ with _ | _
      Â· refine âŸ¨s, âŸ¨r, hr', hrâŸ©, subset_refl _, ?_âŸ©
        have : IsCompact (Units.val â»Â¹' s) := subsingleton_of_subsingleton.isCompact
        convert (this.image Units.continuous_val).insert 0
        ext x
        rcases GroupWithZero.eq_zero_or_unit x with rfl | âŸ¨y, hyâŸ©
        Â· simp only [mem_insert_iff, mem_image, mem_preimage, Units.ne_zero, and_false,
          exists_const, or_false, iff_true]
          refine hr ?_
          simp [zero_lt_iff]
        Â· simp [hy, Units.val_inj]
      obtain âŸ¨z, hzâŸ© : âˆƒ z : Î“â‚€Ë£, (z : Î“â‚€) < 1 := exists_lt 1
      refine âŸ¨Iic (Units.val (min (min z x) r) ^ 2), âŸ¨_, ?_, Iio_subset_Iic_selfâŸ©, hr.trans' ?_,
        h.of_isClosed_subset isClosed_Iic (Iic_subset_Iic.mpr ?_)âŸ©
      Â· simp [â† zero_lt_iff]
      Â· simp only [le_eq_subset, Iic_subset_Iio, Units.val_lt_val, â† Units.val_pow_eq_pow_val]
        refine (pow_lt_self_of_lt_oneâ‚€ (by simp) ?_ one_lt_two).trans_le ?_ <;>
        simp [hz]
      Â· simp only [â† Units.val_pow_eq_pow_val, Units.val_le_val]
        refine (pow_le_of_le_one (by simp) ?_ two_ne_zero).trans ?_ <;>
        simp [hz.le]

lemma locallyCompactSpace_iff_locallyFiniteOrder_units :
    LocallyCompactSpace Î“â‚€ â†” Nonempty (LocallyFiniteOrder Î“â‚€Ë£) := by
  constructor
  Â· intro h
    refine âŸ¨LocallyFiniteOrder.ofFiniteIcc ?_âŸ©
    suffices âˆ€ x : Î“â‚€Ë£, (Icc x 1).Finite by
      rintro x y
      rcases lt_trichotomy y x with hxy | rfl | hxy
      Â· rw [Set.Icc_eq_empty_of_lt]
        Â· exact Set.finite_empty
        Â· simp [hxy]
      Â· simp
      wlog h : x â‰¤ 1 generalizing x y
      Â· push_neg at h
        specialize this yâ»Â¹ xâ»Â¹ (inv_lt_inv' hxy) (inv_le_one_of_one_le (h.trans hxy).le)
        refine (this.image (Â· â»Â¹)).subset ?_
        simp
      generalize_proofs _ _ _ _ hxu hyu
      rcases le_total y 1 with hy | hy
      Â· exact (this x).subset (Set.Icc_subset_Icc_right hy)
      Â· have H : (Set.Icc yâ»Â¹ 1).Finite := this _
        refine ((this x).union H.inv).subset (le_of_eq ?_)
        rw [Set.inv_Icc, inv_one, Set.Icc_union_Icc_eq_Icc] <;>
        simp [h, hy]
    intro z
    obtain âŸ¨t, ht, ht', ht''âŸ© := local_compact_nhds (x := 0) (n := Iio z.val)
      (by simp [hasBasis_nhds_zero.mem_of_mem])
    rw [hasBasis_nhds_zero.mem_iff] at ht
    obtain âŸ¨y, hy', hyâŸ© := ht
    lift y to Î“â‚€Ë£ using IsUnit.mk0 _ hy'
    rw [â† Set.image_subset_image_iff (OrderIso.mulLeftâ‚€ y.valâ»Â¹ (by simp)).injective,
      (OrderIso.mulLeftâ‚€ _ _).image_Iio]
      at hy
    simp only [OrderIso.mulLeftâ‚€_apply, ne_eq, Units.ne_zero, not_false_eq_true,
      inv_mul_cancelâ‚€] at hy
    have : IsCompact (Iic (1 : Î“â‚€)) := by
      refine ((ht''.image (continuous_mul_left y.valâ»Â¹)).insert 1).of_isClosed_subset
        isClosed_Iic ?_
      simp [â† Iio_insert, insert_subset_insert_iff, hy]
    let f : Î“â‚€ â†’ Set Î“â‚€ := fun x â†¦ if x < z then Iio z else {x}
    have := this.elim_finite_subcover f ?_ ?_
    Â· obtain âŸ¨s, hsâŸ© := this
      suffices (Icc z.val 1).Finite from this.preimage (Units.val_injective.injOn)
      refine (s.finite_toSet).subset (
        ((Set.inter_subset_inter_right (Icc z.val 1) hs).trans ?_).trans' ?_)
      Â· intro x
        simp only [mem_inter_iff, mem_Icc, mem_iUnion, mem_ite, mem_Iio, not_lt, mem_singleton_iff,
          exists_and_left, exists_prop, Finset.mem_coe, and_imp, forall_exists_index, f]
        intro hzx
        simp only [hzx.not_gt, imp_false, not_lt]
        grind
      Â· simp [Icc_subset_Iic_self]
    Â· intro x
      simp only [f]
      split_ifs with hx
      Â· exact isOpen_Iio
      Â· refine isOpen_singleton ?_
        push_neg at hx
        rw [â† zero_lt_iff]
        refine hx.trans_lt' ?_
        simp
    Â· intro x
      simp only [mem_iUnion, f]
      intro
      use x
      simp [mem_ite]
  Â· rintro âŸ¨_âŸ©
    apply locallyCompactSpace_of_compact_Iic one_ne_zero
    refine isCompact_of_finite_subcover ?_
    intro Î¹ f hf hs
    choose g hg hg' using hs
    choose i hi using hg
    simp only at hi
    simp only [isOpen_iff, ne_eq] at hf
    obtain âŸ¨y, hy', hyâŸ© := (hf (i zero_le_one)).neg_resolve_left (hi zero_le_one â–¸ hg' zero_le_one)
    classical
    refine âŸ¨{i zero_le_one} âˆª (Finset.Icc (Units.mk0 _ hy') 1).attach.image
      (fun z â†¦ @i z.val ?_), ?_âŸ©
    Â· have := z.prop
      rw [Finset.mem_Icc] at this
      simpa [â† Units.val_lt_val] using this.right
    Â· intro z
      simp only [mem_Iic, Finset.singleton_union, Finset.mem_insert, Finset.mem_image,
        Finset.mem_attach, true_and, Subtype.exists, Finset.mem_Icc, â† Units.val_le_val,
        Units.val_mk0, Units.val_one, iUnion_iUnion_eq_or_left, iUnion_exists, biUnion_and',
        iUnion_iUnion_eq', mem_union, mem_iUnion]
      intro hzx
      rcases lt_or_ge z y with hzy | hzy
      Â· exact Or.inl (hy (by simp [hzy]))
      Â· refine Or.inr âŸ¨Units.mk0 z (ne_of_gt (hzy.trans_lt' (zero_lt_iff.mpr hy'))), ?_âŸ©
        simp [hzy, hzx, hi, hg']

end WithZeroTopology
