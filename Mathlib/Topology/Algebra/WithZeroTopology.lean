/-
Copyright (c) 2021 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
import Mathlib.Algebra.Group.Pointwise.Set.Finite
import Mathlib.Algebra.Order.Group.Pointwise.Interval
import Mathlib.Algebra.Order.Group.Units
import Mathlib.Order.Interval.Finset.Defs
import Mathlib.Topology.Algebra.GroupWithZero
import Mathlib.Topology.Order.OrderClosed

/-!
# The topology on linearly ordered commutative groups with zero

Let `Î“â‚€` be a linearly ordered commutative group to which we have adjoined a zero element.  Then
`Î“â‚€` may naturally be endowed with a topology that turns `Î“â‚€` into a topological monoid.
Neighborhoods of zero are sets containing `{ Î³ | Î³ < Î³â‚€ }` for some invertible element `Î³â‚€` and
every invertible element is open.  In particular the topology is the following: "a subset `U âŠ† Î“â‚€`
is open if `0 âˆ‰ U` or if there is an invertible `Î³â‚€ âˆˆ Î“â‚€` such that `{ Î³ | Î³ < Î³â‚€ } âŠ† U`", see
`WithZeroTopology.isOpen_iff`.

We prove this topology is ordered and Tâ‚… (in addition to be compatible with the monoid
structure).

All this is useful to extend a valuation to a completion. This is an abstract version of how the
absolute value (resp. `p`-adic absolute value) on `â„š` is extended to `â„` (resp. `â„šâ‚š`).

## Implementation notes

This topology is defined as a scoped instance since it may not be the desired topology on
a linearly ordered commutative group with zero. You can locally activate this topology using
`open WithZeroTopology`.
-/

open Topology Filter TopologicalSpace Filter Set Function

namespace WithZeroTopology

variable {Î± Î“â‚€ : Type*} [LinearOrderedCommGroupWithZero Î“â‚€] {Î³ Î³â‚ Î³â‚‚ : Î“â‚€} {l : Filter Î±}
  {f : Î± â†’ Î“â‚€}

/-- The topology on a linearly ordered commutative group with a zero element adjoined.
A subset U is open if 0 âˆ‰ U or if there is an invertible element Î³â‚€ such that {Î³ | Î³ < Î³â‚€} âŠ† U. -/
scoped instance (priority := 100) topologicalSpace : TopologicalSpace Î“â‚€ :=
  nhdsAdjoint 0 <| â¨… Î³ â‰  0, ğ“Ÿ (Iio Î³)

theorem nhds_eq_update : (ğ“ : Î“â‚€ â†’ Filter Î“â‚€) = update pure 0 (â¨… Î³ â‰  0, ğ“Ÿ (Iio Î³)) := by
  rw [nhds_nhdsAdjoint, sup_of_le_right]
  exact le_iInfâ‚‚ fun Î³ hÎ³ â†¦ le_principal_iff.2 <| zero_lt_iff.2 hÎ³

/-!
### Neighbourhoods of zero
-/

theorem nhds_zero : ğ“ (0 : Î“â‚€) = â¨… Î³ â‰  0, ğ“Ÿ (Iio Î³) := by
  rw [nhds_eq_update, update_self]

/-- In a linearly ordered group with zero element adjoined, `U` is a neighbourhood of `0` if and
only if there exists a nonzero element `Î³â‚€` such that `Iio Î³â‚€ âŠ† U`. -/
theorem hasBasis_nhds_zero : (ğ“ (0 : Î“â‚€)).HasBasis (fun Î³ : Î“â‚€ => Î³ â‰  0) Iio := by
  rw [nhds_zero]
  refine hasBasis_biInf_principal ?_ âŸ¨1, one_ne_zeroâŸ©
  exact directedOn_iff_directed.2 (Monotone.directed_ge fun a b hab => Iio_subset_Iio hab)

theorem Iio_mem_nhds_zero (hÎ³ : Î³ â‰  0) : Iio Î³ âˆˆ ğ“ (0 : Î“â‚€) :=
  hasBasis_nhds_zero.mem_of_mem hÎ³

/-- If `Î³` is an invertible element of a linearly ordered group with zero element adjoined, then
`Iio (Î³ : Î“â‚€)` is a neighbourhood of `0`. -/
theorem nhds_zero_of_units (Î³ : Î“â‚€Ë£) : Iio â†‘Î³ âˆˆ ğ“ (0 : Î“â‚€) :=
  Iio_mem_nhds_zero Î³.ne_zero

theorem tendsto_zero : Tendsto f l (ğ“ (0 : Î“â‚€)) â†” âˆ€ (Î³â‚€) (_ : Î³â‚€ â‰  0), âˆ€á¶  x in l, f x < Î³â‚€ := by
  simp [nhds_zero]

/-!
### Neighbourhoods of non-zero elements
-/

/-- The neighbourhood filter of a nonzero element consists of all sets containing that
element. -/
@[simp]
theorem nhds_of_ne_zero {Î³ : Î“â‚€} (hâ‚€ : Î³ â‰  0) : ğ“ Î³ = pure Î³ :=
  nhds_nhdsAdjoint_of_ne _ hâ‚€

/-- The neighbourhood filter of an invertible element consists of all sets containing that
element. -/
theorem nhds_coe_units (Î³ : Î“â‚€Ë£) : ğ“ (Î³ : Î“â‚€) = pure (Î³ : Î“â‚€) :=
  nhds_of_ne_zero Î³.ne_zero

/-- If `Î³` is an invertible element of a linearly ordered group with zero element adjoined, then
`{Î³}` is a neighbourhood of `Î³`. -/
theorem singleton_mem_nhds_of_units (Î³ : Î“â‚€Ë£) : ({â†‘Î³} : Set Î“â‚€) âˆˆ ğ“ (Î³ : Î“â‚€) := by simp

/-- If `Î³` is a nonzero element of a linearly ordered group with zero element adjoined, then `{Î³}`
is a neighbourhood of `Î³`. -/
theorem singleton_mem_nhds_of_ne_zero (h : Î³ â‰  0) : ({Î³} : Set Î“â‚€) âˆˆ ğ“ (Î³ : Î“â‚€) := by simp [h]

theorem hasBasis_nhds_of_ne_zero {x : Î“â‚€} (h : x â‰  0) :
    HasBasis (ğ“ x) (fun _ : Unit => True) fun _ => {x} := by
  rw [nhds_of_ne_zero h]
  exact hasBasis_pure _

theorem hasBasis_nhds_units (Î³ : Î“â‚€Ë£) :
    HasBasis (ğ“ (Î³ : Î“â‚€)) (fun _ : Unit => True) fun _ => {â†‘Î³} :=
  hasBasis_nhds_of_ne_zero Î³.ne_zero

theorem tendsto_of_ne_zero {Î³ : Î“â‚€} (h : Î³ â‰  0) : Tendsto f l (ğ“ Î³) â†” âˆ€á¶  x in l, f x = Î³ := by
  rw [nhds_of_ne_zero h, tendsto_pure]

theorem tendsto_units {Î³â‚€ : Î“â‚€Ë£} : Tendsto f l (ğ“ (Î³â‚€ : Î“â‚€)) â†” âˆ€á¶  x in l, f x = Î³â‚€ :=
  tendsto_of_ne_zero Î³â‚€.ne_zero

theorem Iio_mem_nhds (h : Î³â‚ < Î³â‚‚) : Iio Î³â‚‚ âˆˆ ğ“ Î³â‚ := by
  rcases eq_or_ne Î³â‚ 0 with (rfl | hâ‚€) <;> simp [*, h.ne', Iio_mem_nhds_zero]

/-!
### Open/closed sets
-/

theorem isOpen_iff {s : Set Î“â‚€} : IsOpen s â†” (0 : Î“â‚€) âˆ‰ s âˆ¨ âˆƒ Î³, Î³ â‰  0 âˆ§ Iio Î³ âŠ† s := by
  rw [isOpen_iff_mem_nhds, â† and_forall_ne (0 : Î“â‚€)]
  simp +contextual [nhds_of_ne_zero, imp_iff_not_or,
    hasBasis_nhds_zero.mem_iff]

theorem isClosed_iff {s : Set Î“â‚€} : IsClosed s â†” (0 : Î“â‚€) âˆˆ s âˆ¨ âˆƒ Î³, Î³ â‰  0 âˆ§ s âŠ† Ici Î³ := by
  simp only [â† isOpen_compl_iff, isOpen_iff, mem_compl_iff, not_not, â† compl_Ici,
    compl_subset_compl]

theorem isOpen_Iio {a : Î“â‚€} : IsOpen (Iio a) :=
  isOpen_iff.mpr <| imp_iff_not_or.mp fun ha => âŸ¨a, ne_of_gt ha, Subset.rflâŸ©

lemma isOpen_singleton (h : Î³ â‰  0) : IsOpen {Î³} := isOpen_singleton_nhdsAdjoint _ h

/-!
### Instances
-/

/-- The topology on a linearly ordered group with zero element adjoined is compatible with the order
structure: the set `{p : Î“â‚€ Ã— Î“â‚€ | p.1 â‰¤ p.2}` is closed. -/
@[nolint defLemma]
scoped instance (priority := 100) orderClosedTopology : OrderClosedTopology Î“â‚€ where
  isClosed_le' := by
    simp only [â† isOpen_compl_iff, compl_setOf, not_le, isOpen_iff_mem_nhds]
    rintro âŸ¨a, bâŸ© (hab : b < a)
    rw [nhds_prod_eq, nhds_of_ne_zero (zero_le'.trans_lt hab).ne', pure_prod]
    exact Iio_mem_nhds hab

/-- The topology on a linearly ordered group with zero element adjoined is Tâ‚…. -/
@[nolint defLemma]
scoped instance (priority := 100) t5Space : T5Space Î“â‚€ where
  completely_normal := fun s t hâ‚ hâ‚‚ => by
    by_cases hs : 0 âˆˆ s
    Â· have ht : 0 âˆ‰ t := fun ht => disjoint_left.1 hâ‚ (subset_closure hs) ht
      rwa [(isOpen_iff.2 (.inl ht)).nhdsSet_eq, disjoint_nhdsSet_principal]
    Â· rwa [(isOpen_iff.2 (.inl hs)).nhdsSet_eq, disjoint_principal_nhdsSet]

/-- The topology on a linearly ordered group with zero element adjoined makes it a topological
monoid. -/
@[nolint defLemma]
scoped instance (priority := 100) : ContinuousMul Î“â‚€ where
  continuous_mul := by
    simp only [continuous_iff_continuousAt, ContinuousAt]
    rintro âŸ¨x, yâŸ©
    wlog hle : x â‰¤ y generalizing x y
    Â· have := (this y x (le_of_not_ge hle)).comp (continuous_swap.tendsto (x, y))
      simpa only [mul_comm, Function.comp_def, Prod.swap] using this
    rcases eq_or_ne x 0 with (rfl | hx) <;> [rcases eq_or_ne y 0 with (rfl | hy); skip]
    Â· rw [zero_mul]
      refine ((hasBasis_nhds_zero.prod_nhds hasBasis_nhds_zero).tendsto_iff hasBasis_nhds_zero).2
        fun Î³ hÎ³ => âŸ¨(Î³, 1), âŸ¨hÎ³, one_ne_zeroâŸ©, ?_âŸ©
      rintro âŸ¨x, yâŸ© âŸ¨hx : x < Î³, hy : y < 1âŸ©
      exact (mul_lt_mul'' hx hy zero_le' zero_le').trans_eq (mul_one Î³)
    Â· rw [zero_mul, nhds_prod_eq, nhds_of_ne_zero hy, prod_pure, tendsto_map'_iff]
      refine (hasBasis_nhds_zero.tendsto_iff hasBasis_nhds_zero).2 fun Î³ hÎ³ => ?_
      refine âŸ¨Î³ / y, div_ne_zero hÎ³ hy, fun x hx => ?_âŸ©
      calc x * y < Î³ / y * y := mul_lt_mul_of_pos_right hx (zero_lt_iff.2 hy)
      _ = Î³ := div_mul_cancelâ‚€ _ hy
    Â· have hy : y â‰  0 := ((zero_lt_iff.mpr hx).trans_le hle).ne'
      rw [nhds_prod_eq, nhds_of_ne_zero hx, nhds_of_ne_zero hy, prod_pure_pure]
      exact pure_le_nhds (x * y)

@[nolint defLemma]
scoped instance (priority := 100) : HasContinuousInvâ‚€ Î“â‚€ :=
  âŸ¨fun Î³ h => by
    rw [ContinuousAt, nhds_of_ne_zero h]
    exact pure_le_nhds Î³â»Â¹âŸ©

instance : DiscreteTopology { g : Î“â‚€ // g â‰  0 } := by
  simpa [discreteTopology_iff_singleton_mem_nhds, nhds_induced] using by aesop

instance : DiscreteTopology Î“â‚€Ë£ :=
  unitsHomeomorphNeZero.symm.discreteTopology

theorem isInducing_unitsVal : IsInducing (Units.val : Î“â‚€Ë£ â†’ Î“â‚€) :=
  by simp [isInducing_iff_nhds, â† image_singleton, Units.val_injective.preimage_image]

lemma isOpenEmbedding_units_val : IsOpenEmbedding (Units.val : Î“â‚€Ë£ â†’ Î“â‚€) where
  eq_induced := isInducing_unitsVal.1
  injective := Units.val_injective
  isOpen_range := by simp [isOpen_iff]

lemma locallyCompactSpace_of_compact_Iio (h : IsCompact (Iio (1 : Î“â‚€))) :
    LocallyCompactSpace Î“â‚€ := by
  have key (x : Î“â‚€) : (ğ“ x).HasBasis (fun r : Î“â‚€ â†¦ x = 0 â†’ r â‰  0)
      fun r â†¦ if x = 0 then Iio r else {x} := by
    split_ifs with h
    Â· simpa [h] using hasBasis_nhds_zero
    Â· simpa [h] using (Filter.hasBasis_pure x).to_hasBasis (by simp) (by simp)
  refine LocallyCompactSpace.of_hasBasis key fun r i hr â†¦ ?_
  split_ifs with hr0
  Â· convert h.image (continuous_mul_left i)
    rw [image_mul_left_Iio (zero_lt_iff.mpr <| hr hr0), mul_one]
  Â· exact isCompact_singleton

theorem compact_Iio_of_locallyCompactSpace [LocallyCompactSpace Î“â‚€] (r : Î“â‚€) :
    IsCompact (Iio r) := by
  obtain rfl | hr0 := eq_or_ne r 0
  Â· simp [â† bot_eq_zero'']
  obtain âŸ¨s, hs0, _, hcsâŸ© := local_compact_nhds <| Iio_mem_nhds_zero (Î“â‚€ := Î“â‚€) one_ne_zero
  obtain âŸ¨w, hw0, hwsâŸ© := hasBasis_nhds_zero.mem_iff.mp hs0
  convert (hcs.of_isClosed_subset (by simp_all [isClosed_iff, zero_lt_iff]) hws).image
    (continuous_mul_left (r / w))
  rw [image_mul_left_Iio (by simp_all [zero_lt_iff]), div_mul_cancelâ‚€ _ hw0]

theorem compact_Iic_of_locallyCompactSpace [LocallyCompactSpace Î“â‚€] (r : Î“â‚€) :
    IsCompact (Iic r) :=
  Set.Iio_insert (a := r) â–¸ (compact_Iio_of_locallyCompactSpace r).insert _

theorem compact_Icc_of_locallyCompactSpace [LocallyCompactSpace Î“â‚€] (x y : Î“â‚€) :
    IsCompact (Icc x y) :=
  (compact_Iic_of_locallyCompactSpace y).of_isClosed_subset isClosed_Icc Icc_subset_Iic_self

lemma locallyCompactSpace_iff_locallyFiniteOrder_units :
    LocallyCompactSpace Î“â‚€ â†” Nonempty (LocallyFiniteOrder Î“â‚€Ë£) := by
  -- `[x, y]` is compact, but `Î“â‚€Ë£` is discrete, so `[x, y]` is finite.
  constructor
  Â· intro h
    refine âŸ¨LocallyFiniteOrder.ofFiniteIcc fun x y â†¦ IsCompact.finite_of_discrete <|
      isInducing_unitsVal.isCompact_iff.mpr ?_âŸ©
    convert compact_Icc_of_locallyCompactSpace x.val y.val
    exact Set.ext fun z â†¦ âŸ¨fun âŸ¨z', h1, h2âŸ© â†¦ h2 â–¸ h1,
      fun h â†¦ âŸ¨.mk0 z <| ne_of_gt <| x.zero_lt.trans_le h.1, h, rflâŸ©âŸ©
  Â· rintro âŸ¨_âŸ©
    -- it suffices to show that `[0, 1)` is compact
    refine locallyCompactSpace_of_compact_Iio ?_
    -- for that, it suffices to show that the cofinite filter on `{ Î³ : Î“â‚€Ë£ | Î³ < 1 }` tends to 0
    let c : Iio (1 : Î“â‚€Ë£) â†’ Î“â‚€ := (Â·.1.1)
    have : Tendsto c cofinite (ğ“ 0) := fun s hs â†¦ by
      obtain âŸ¨r, hr0, hrsâŸ© := hasBasis_nhds_zero.mem_iff.mp hs
      refine .of_finite_image ((finite_Ico (.mk0 r hr0) 1).subset ?_) Subtype.val_injective.injOn
      exact fun y âŸ¨x, hx, hxyâŸ© â†¦ hxy â–¸ âŸ¨le_of_not_gt fun hxr â†¦ hx <| hrs hxr, x.2âŸ©
    convert this.isCompact_insert_range_of_cofinite
    exact Set.ext fun x â†¦ âŸ¨fun hx â†¦ or_iff_not_imp_left.mpr fun hx0 â†¦ âŸ¨âŸ¨.mk0 x hx0, hxâŸ©, rflâŸ©,
      Or.rec (Â· â–¸ zero_lt_one) fun âŸ¨y, hxyâŸ© â†¦ hxy â–¸ y.2âŸ©

end WithZeroTopology
