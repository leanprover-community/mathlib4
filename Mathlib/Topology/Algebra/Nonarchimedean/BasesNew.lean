/-
Copyright (c) 2021 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Anatole Dedecker
-/
import Mathlib.Topology.Algebra.Nonarchimedean.Basic
import Mathlib.Topology.Algebra.FilterBasisNew

/-!
**WARNING** : This is a new version of `Topology.Algebra.Nonarchimedean.Bases`, which is still used
by the library at this point in time. The library will be gradually modified to use this file
instead. See `https://leanprover.zulipchat.com/#narrow/channel/287929-mathlib4/topic/Refactoring.20algebraic.20filter.20bases/near/479437345`
for more details.

# Neighborhood bases for non-archimedean groups, rings and modules

This file specializes the theory of algebraic filter bases (see
the docstring of `Topology.Algebra.FilterBasis`) to filter bases whose elements are
algebraic subobjects.

More specifically, we provide multiple alternative constructors for `Filter.IsXBasis`,
where `X ‚àà {Group, AddGroup, Ring, Module}`, in the case where the basis elements
have some extra algebraic structure. For example, `Filter.IsAddGroupBasis.mk_of_subgroups_of_comm`
shows that, in a commutative additive group, any filter basis made of subgroups is automatically
a group filter basis.

We also prove some extra results about the topological objects obtained in this setting
(that is, nonarchimedean objects), but these should probably be tweaked to *not* mention
`IsGroupBasis` (and friends) and moved elsewhere. More details in comments around the file.

## Implementation note

* We use subobject classes, so for example `Filter.IsAddGroupBasis.mk_of_subgroups` will
  also apply if your basis is valued in `Submodule`, `Ideal`, ...

* For a long time, each additional constructor in this file was its own predicate (e.g
  there was a definition for "ring filter bases made of subgroups", another one for
  "ring filter bases made of submodules", and so on...). Furthermore, these did not take any
  predicate bounding the filter basis, making them inconsistent with both `Filter.IsBasis`
  and `FilterBasis` (recall that the general setup used to rely on `FilterBasis`).
  This led to a lot of (inconsistent) code duplication, which doesn't exist anymore.

-/

open Set Filter Function Lattice

open Topology Filter Pointwise

namespace Filter

namespace IsGroupBasis

/-- A simpler constructor for `Filter.IsGroupBasis` for bases valued in
`Subgroup` (or any `S` satisfying `SubgroupClass S G`). -/
@[to_additive "A simpler constructor for `Filter.IsAddGroupBasis` for bases valued in
`AddSubgroup` (or any `S` satisfying `AddSubgroupClass S G`)."]
theorem mk_of_subgroups {G S : Type*} {Œπ : Sort*} [Group G] [SetLike S G] [SubgroupClass S G]
    {p : Œπ ‚Üí Prop} {B : Œπ ‚Üí S} (isBasis : IsBasis p (fun i ‚Ü¶ B i : Œπ ‚Üí Set G))
    (conj : ‚àÄ x‚ÇÄ, ‚àÄ {i}, p i ‚Üí ‚àÉ j, p j ‚àß MapsTo (x‚ÇÄ * ¬∑ * x‚ÇÄ‚Åª¬π) (B j) (B i : Set G)) :
    IsGroupBasis p (fun i ‚Ü¶ B i : Œπ ‚Üí Set G) where
  toIsBasis := isBasis
  one _ := one_mem _
  mul {i} hi := ‚ü®i, hi, mul_subset_iff.mpr fun _ ha _ hb ‚Ü¶ mul_mem ha hb‚ü©
  inv {i} hi := ‚ü®i, hi, fun _ ha ‚Ü¶ inv_mem ha‚ü©
  conj := conj

/-- A version of `Filter.IsGroupBasis.mk_of_subgroups` for commutative groups. -/
@[to_additive "A version of `Filter.IsAddGroupBasis.mk_of_subgroups` for commutative groups."]
theorem mk_of_subgroups_of_comm {G S : Type*} {Œπ : Sort*} [CommGroup G]
    [SetLike S G] [SubgroupClass S G] {p : Œπ ‚Üí Prop} {B : Œπ ‚Üí S}
    (isBasis : IsBasis p (fun i ‚Ü¶ B i : Œπ ‚Üí Set G)) :
    IsGroupBasis p (fun i ‚Ü¶ B i : Œπ ‚Üí Set G) :=
  .mk_of_comm _ _ isBasis (fun _ ‚Ü¶ one_mem _)
    (fun {i} hi ‚Ü¶ ‚ü®i, hi, mul_subset_iff.mpr fun _ ha _ hb ‚Ü¶ mul_mem ha hb‚ü©)
    (fun {i} hi ‚Ü¶ ‚ü®i, hi, fun _ ha ‚Ü¶ inv_mem ha‚ü©)

variable {G S : Type*} {Œπ : Sort*} [Group G] [SetLike S G] [SubgroupClass S G]
  {p : Œπ ‚Üí Prop} {B : Œπ ‚Üí S} (hB : IsGroupBasis p (fun i ‚Ü¶ B i : Œπ ‚Üí Set G))
include hB

-- TODO(Anatole) : this should be a general lemma assuming `(ùìù 1).HasBasis p s`
-- with `s` valued in subgroups.
/-- If a group filter basis is made of subgroups, these are open for the associated topology. -/
@[to_additive "If a group filter basis is made of subgroups, these are open for the associated
topology."]
def openSubgroup_of_subgroups {i : Œπ} (hi : p i) :
    @OpenSubgroup G _ hB.topology :=
  -- Porting note: failed to synthesize instance `TopologicalSpace G`
  letI := hB.topology
  { carrier := B i
    mul_mem' := mul_mem
    one_mem' := one_mem _
    inv_mem' := inv_mem
    isOpen' := Subgroup.isOpen_of_mem_nhds (.ofClass (B i)) (hB.nhds_one_hasBasis.mem_of_mem hi) }

-- TODO(Anatole) : this should be a general lemma assuming `(ùìù 1).HasBasis p s`
-- with `s` valued in subgroups.
-- see Note [nonarchimedean non instances]
@[to_additive]
theorem nonarchimedean_of_subgroups : @NonarchimedeanGroup G _ hB.topology := by
  letI := hB.topology
  constructor
  intro U hU
  obtain ‚ü®i, hi, hiU : (B i : Set G) ‚äÜ U‚ü© := hB.nhds_one_hasBasis.mem_iff.mp hU
  exact ‚ü®hB.openSubgroup_of_subgroups hi, hiU‚ü©

end IsGroupBasis

namespace IsRingBasis

/-- A simpler constructor for `Filter.IsRingBasis` for bases valued in
`AddSubgroup` (or any `S` satisfying `AddSubgroupClass S A`). -/
theorem mk_of_subgroups {A S : Type*} {Œπ : Sort*} [Ring A] [SetLike S A] [AddSubgroupClass S A]
    {p : Œπ ‚Üí Prop} {B : Œπ ‚Üí S} (isBasis : IsBasis p (fun i ‚Ü¶ B i : Œπ ‚Üí Set A))
    (mul : ‚àÄ {i}, p i ‚Üí ‚àÉ j, p j ‚àß (B j : Set A) * B j ‚äÜ B i)
    (mul_left : ‚àÄ x : A, ‚àÄ {i}, p i ‚Üí ‚àÉ j, p j ‚àß MapsTo (x * ¬∑) (B j) (B i))
    (mul_right : ‚àÄ x : A, ‚àÄ {i}, p i ‚Üí ‚àÉ j, p j ‚àß MapsTo (¬∑ * x) (B j) (B i)) :
    IsRingBasis p (fun i ‚Ü¶ B i : Œπ ‚Üí Set A) where
  toIsAddGroupBasis := .mk_of_subgroups_of_comm isBasis
  mul := mul
  mul_left := mul_left
  mul_right := mul_right

/-- A version of `Filter.IsRingBasis.mk_of_subgroups` in the commutative case. -/
theorem mk_of_subgroups_of_comm {A S : Type*} {Œπ : Sort*} [CommRing A]
    [SetLike S A] [AddSubgroupClass S A] {p : Œπ ‚Üí Prop} {B : Œπ ‚Üí S}
    (isBasis : IsBasis p (fun i ‚Ü¶ B i : Œπ ‚Üí Set A))
    (mul : ‚àÄ {i}, p i ‚Üí ‚àÉ j, p j ‚àß (B j : Set A) * B j ‚äÜ B i)
    (mul_left : ‚àÄ x : A, ‚àÄ {i}, p i ‚Üí ‚àÉ j, p j ‚àß MapsTo (x * ¬∑) (B j) (B i)) :
    IsRingBasis p (fun i ‚Ü¶ B i : Œπ ‚Üí Set A) :=
  .mk_of_comm _ _ (.mk_of_subgroups_of_comm isBasis) mul mul_left

/-- A simpler constructor for `Filter.IsRingBasis` for bases valued in
`Ideal` (or any `S` satisfying `AddSubgroupClass S A` and `SMulMemClass S A A`). -/
theorem mk_of_ideals_of_comm {A S : Type*} {Œπ : Sort*} [CommRing A]
    [SetLike S A] [AddSubgroupClass S A] [SMulMemClass S A A] {p : Œπ ‚Üí Prop} {B : Œπ ‚Üí S}
    (isBasis : IsBasis p (fun i ‚Ü¶ B i : Œπ ‚Üí Set A)) :
    IsRingBasis p (fun i ‚Ü¶ B i : Œπ ‚Üí Set A) :=
  .mk_of_subgroups_of_comm isBasis
    (fun {i} hi ‚Ü¶ ‚ü®i, hi, mul_subset_iff.mpr fun a _ _ hb ‚Ü¶ SMulMemClass.smul_mem a hb‚ü©)
    (fun a {i} hi ‚Ü¶ ‚ü®i, hi, fun _ hx ‚Ü¶ SMulMemClass.smul_mem a hx‚ü©)

variable {A S : Type*} {Œπ : Sort*} [Ring A] [SetLike S A] [AddSubgroupClass S A]
    {p : Œπ ‚Üí Prop} {B : Œπ ‚Üí S} (hB : IsRingBasis p (fun i ‚Ü¶ B i : Œπ ‚Üí Set A))

-- TODO(Anatole) : this should be a general lemma assuming `(ùìù 0).HasBasis p s`
-- with `s` valued in subgroups.
-- see Note [nonarchimedean non instances]
nonrec theorem nonarchimedean_of_subgroups : @NonarchimedeanRing A _ hB.topology := by
  letI := hB.topology
  constructor
  exact hB.nonarchimedean_of_subgroups.is_nonarchimedean

end IsRingBasis

namespace IsModuleBasis

/-- A simpler constructor for `Filter.IsModuleBasis` for bases valued in
`Submodule` (or any `S` satisfying `AddSubgroupClass S M` and `SMulMemClass S R M`). -/
theorem mk_of_submodules {R M S : Type*} {Œπ : Sort*} [Ring R]
    [TopologicalSpace R] [AddCommGroup M] [Module R M]
    [SetLike S M] [AddSubgroupClass S M] [SMulMemClass S R M]
    {p : Œπ ‚Üí Prop} {B : Œπ ‚Üí S}
    (isBasis : IsBasis p (fun i ‚Ü¶ B i : Œπ ‚Üí Set M))
    (smul : ‚àÄ (m : M) {i : Œπ}, p i ‚Üí ‚àÄ·∂† a in ùìù (0 : R), a ‚Ä¢ m ‚àà B i) :
    IsModuleBasis R p (fun i ‚Ü¶ B i : Œπ ‚Üí Set M) where
  toIsAddGroupBasis := .mk_of_subgroups_of_comm isBasis
  smul {i} hi := ‚ü®univ, univ_mem, i, hi, smul_subset_iff.mpr
    fun _ _ _ hb ‚Ü¶ SMulMemClass.smul_mem _ hb‚ü©
  smul_left _ {i} hi := ‚ü®i, hi, fun _ hb ‚Ü¶ SMulMemClass.smul_mem _ hb‚ü©
  smul_right := smul

set_option linter.unusedVariables.funArgs false
/-- A version of `IsModuleBasis.mk_of_submodules` given a preferred basis of neighborhoods of zero
in the base ring. In particular, this applies when the ring topology comes from
`Filter.IsRingBasis`. -/
theorem mk_of_submodules_of_hasBasis {R M S : Type*} {ŒπR ŒπM : Sort*} [CommRing R]
    [tR : TopologicalSpace R] [AddCommGroup M] [Module R M]
    [SetLike S M] [AddSubgroupClass S M] [SMulMemClass S R M]
    {pR : ŒπR ‚Üí Prop} {sR : ŒπR ‚Üí Set R} (hR : (ùìù 0).HasBasis pR sR)
    {pM : ŒπM ‚Üí Prop} {sM : ŒπM ‚Üí S} (isBasis : IsBasis pM (fun i ‚Ü¶ sM i : ŒπM ‚Üí Set M))
    (smul : ‚àÄ (m : M) {i}, pM i ‚Üí ‚àÉ j, pR j ‚àß MapsTo (¬∑ ‚Ä¢ m) (sR j) (sM i)) :
    IsModuleBasis R pM (fun i ‚Ü¶ sM i : ŒπM ‚Üí Set M) :=
  .mk_of_submodules isBasis (fun m‚ÇÄ _ hi ‚Ü¶ hR.eventually_iff.mpr <| smul m‚ÇÄ hi)

end IsModuleBasis

end Filter
