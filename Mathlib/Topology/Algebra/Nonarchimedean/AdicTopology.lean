/-
Copyright (c) 2021 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
import Mathlib.RingTheory.Ideal.Operations
import Mathlib.Topology.Algebra.Nonarchimedean.Bases
import Mathlib.Topology.UniformSpace.Completion
import Mathlib.Topology.Algebra.UniformRing

#align_import topology.algebra.nonarchimedean.adic_topology from "leanprover-community/mathlib"@"f0c8bf9245297a541f468be517f1bde6195105e9"

/-!
# Adic topology

Given a commutative ring `R` and an ideal `I` in `R`, this file constructs the unique
topology on `R` which is compatible with the ring structure and such that a set is a neighborhood
of zero if and only if it contains a power of `I`. This topology is non-archimedean: every
neighborhood of zero contains an open subgroup, namely a power of `I`.

It also studies the predicate `IsAdic` which states that a given topological ring structure is
adic, proving a characterization and showing that raising an ideal to a positive power does not
change the associated topology.

Finally, it defines `WithIdeal`, a class registering an ideal in a ring and providing the
corresponding adic topology to the type class inference system.


## Main definitions and results

* `Ideal.adic_basis`: the basis of submodules given by powers of an ideal.
* `Ideal.adicTopology`: the adic topology associated to an ideal. It has the above basis
  for neighborhoods of zero.
* `Ideal.nonarchimedean`: the adic topology is non-archimedean
* `isAdic_iff`: A topological ring is `J`-adic if and only if it admits the powers of `J` as
  a basis of open neighborhoods of zero.
* `WithIdeal`: a class registering an ideal in a ring.

## Implementation notes

The `I`-adic topology on a ring `R` has a contrived definition using `I^n â€¢ âŠ¤` instead of `I`
to make sure it is definitionally equal to the `I`-topology on `R` seen as an `R`-module.

-/


variable {R : Type*} [CommRing R]

open Set TopologicalAddGroup Submodule Filter

open Topology Pointwise

namespace Ideal

theorem adic_basis (I : Ideal R) : SubmodulesRingBasis fun n : â„• => (I ^ n â€¢ âŠ¤ : Ideal R) :=
  { inter := by
      suffices âˆ€ i j : â„•, âˆƒ k, I ^ k â‰¤ I ^ i âˆ§ I ^ k â‰¤ I ^ j by
        simpa only [smul_eq_mul, mul_top, Algebra.id.map_eq_id, map_id, le_inf_iff] using this
      intro i j
      -- âŠ¢ âˆƒ k, I ^ k â‰¤ I ^ i âˆ§ I ^ k â‰¤ I ^ j
      exact âŸ¨max i j, pow_le_pow (le_max_left i j), pow_le_pow (le_max_right i j)âŸ©
      -- ğŸ‰ no goals
    leftMul := by
      suffices âˆ€ (a : R) (i : â„•), âˆƒ j : â„•, a â€¢ I ^ j â‰¤ I ^ i by
        simpa only [smul_top_eq_map, Algebra.id.map_eq_id, map_id] using this
      intro r n
      -- âŠ¢ âˆƒ j, r â€¢ I ^ j â‰¤ I ^ n
      use n
      -- âŠ¢ r â€¢ I ^ n â‰¤ I ^ n
      rintro a âŸ¨x, hx, rflâŸ©
      -- âŠ¢ â†‘(DistribMulAction.toLinearMap R R r) x âˆˆ I ^ n
      exact (I ^ n).smul_mem r hx
      -- ğŸ‰ no goals
    mul := by
      suffices âˆ€ i : â„•, âˆƒ j : â„•, (I ^ j: Set R) * (I ^ j : Set R) âŠ† (I ^ i : Set R) by
        simpa only [smul_top_eq_map, Algebra.id.map_eq_id, map_id] using this
      intro n
      -- âŠ¢ âˆƒ j, â†‘(I ^ j) * â†‘(I ^ j) âŠ† â†‘(I ^ n)
      use n
      -- âŠ¢ â†‘(I ^ n) * â†‘(I ^ n) âŠ† â†‘(I ^ n)
      rintro a âŸ¨x, b, _hx, hb, rflâŸ©
      -- âŠ¢ (fun x x_1 => x * x_1) x b âˆˆ â†‘(I ^ n)
      exact (I ^ n).smul_mem x hb }
      -- ğŸ‰ no goals
#align ideal.adic_basis Ideal.adic_basis

/-- The adic ring filter basis associated to an ideal `I` is made of powers of `I`. -/
def ringFilterBasis (I : Ideal R) :=
  I.adic_basis.toRing_subgroups_basis.toRingFilterBasis
#align ideal.ring_filter_basis Ideal.ringFilterBasis

/-- The adic topology associated to an ideal `I`. This topology admits powers of `I` as a basis of
neighborhoods of zero. It is compatible with the ring structure and is non-archimedean. -/
def adicTopology (I : Ideal R) : TopologicalSpace R :=
  (adic_basis I).topology
#align ideal.adic_topology Ideal.adicTopology

theorem nonarchimedean (I : Ideal R) : @NonarchimedeanRing R _ I.adicTopology :=
  I.adic_basis.toRing_subgroups_basis.nonarchimedean
#align ideal.nonarchimedean Ideal.nonarchimedean

/-- For the `I`-adic topology, the neighborhoods of zero has basis given by the powers of `I`. -/
theorem hasBasis_nhds_zero_adic (I : Ideal R) :
    HasBasis (@nhds R I.adicTopology (0 : R)) (fun _n : â„• => True) fun n =>
      ((I ^ n : Ideal R) : Set R) :=
  âŸ¨by
    intro U
    -- âŠ¢ U âˆˆ ğ“ 0 â†” âˆƒ i, True âˆ§ â†‘(I ^ i) âŠ† U
    rw [I.ringFilterBasis.toAddGroupFilterBasis.nhds_zero_hasBasis.mem_iff]
    -- âŠ¢ (âˆƒ i, i âˆˆ RingFilterBasis.toAddGroupFilterBasis âˆ§ id i âŠ† U) â†” âˆƒ i, True âˆ§ â†‘( â€¦
    constructor
    -- âŠ¢ (âˆƒ i, i âˆˆ RingFilterBasis.toAddGroupFilterBasis âˆ§ id i âŠ† U) â†’ âˆƒ i, True âˆ§ â†‘( â€¦
    Â· rintro âŸ¨-, âŸ¨i, rflâŸ©, hâŸ©
      -- âŠ¢ âˆƒ i, True âˆ§ â†‘(I ^ i) âŠ† U
      replace h : â†‘(I ^ i) âŠ† U := by simpa using h
      -- âŠ¢ âˆƒ i, True âˆ§ â†‘(I ^ i) âŠ† U
      exact âŸ¨i, trivial, hâŸ©
      -- ğŸ‰ no goals
    Â· rintro âŸ¨i, -, hâŸ©
      -- âŠ¢ âˆƒ i, i âˆˆ RingFilterBasis.toAddGroupFilterBasis âˆ§ id i âŠ† U
      exact âŸ¨(I ^ i : Ideal R), âŸ¨i, by simpâŸ©, hâŸ©âŸ©
      -- ğŸ‰ no goals
#align ideal.has_basis_nhds_zero_adic Ideal.hasBasis_nhds_zero_adic

theorem hasBasis_nhds_adic (I : Ideal R) (x : R) :
    HasBasis (@nhds R I.adicTopology x) (fun _n : â„• => True) fun n =>
      (fun y => x + y) '' (I ^ n : Ideal R) := by
  letI := I.adicTopology
  -- âŠ¢ HasBasis (ğ“ x) (fun _n => True) fun n => (fun y => x + y) '' â†‘(I ^ n)
  have := I.hasBasis_nhds_zero_adic.map fun y => x + y
  -- âŠ¢ HasBasis (ğ“ x) (fun _n => True) fun n => (fun y => x + y) '' â†‘(I ^ n)
  rwa [map_add_left_nhds_zero x] at this
  -- ğŸ‰ no goals
#align ideal.has_basis_nhds_adic Ideal.hasBasis_nhds_adic

variable (I : Ideal R) (M : Type*) [AddCommGroup M] [Module R M]

theorem adic_module_basis :
    I.ringFilterBasis.SubmodulesBasis fun n : â„• => I ^ n â€¢ (âŠ¤ : Submodule R M) :=
  { inter := fun i j =>
      âŸ¨max i j,
        le_inf_iff.mpr
          âŸ¨smul_mono_left <| pow_le_pow (le_max_left i j),
            smul_mono_left <| pow_le_pow (le_max_right i j)âŸ©âŸ©
    smul := fun m i =>
      âŸ¨(I ^ i â€¢ âŠ¤ : Ideal R), âŸ¨i, by simpâŸ©, fun a a_in => by
                                     -- ğŸ‰ no goals
        replace a_in : a âˆˆ I ^ i := by simpa [(I ^ i).mul_top] using a_in
        -- âŠ¢ a âˆˆ (fun x => x â€¢ m) â»Â¹' â†‘(I ^ i â€¢ âŠ¤)
        exact smul_mem_smul a_in mem_topâŸ© }
        -- ğŸ‰ no goals
#align ideal.adic_module_basis Ideal.adic_module_basis

/-- The topology on an `R`-module `M` associated to an ideal `M`. Submodules $I^n M$,
written `I^n â€¢ âŠ¤` form a basis of neighborhoods of zero. -/
def adicModuleTopology : TopologicalSpace M :=
  @ModuleFilterBasis.topology R M _ I.adic_basis.topology _ _
    (I.ringFilterBasis.moduleFilterBasis (I.adic_module_basis M))
#align ideal.adic_module_topology Ideal.adicModuleTopology

/-- The elements of the basis of neighborhoods of zero for the `I`-adic topology
on an `R`-module `M`, seen as open additive subgroups of `M`. -/
def openAddSubgroup (n : â„•) : @OpenAddSubgroup R _ I.adicTopology := by
  letI := I.adicTopology
  -- âŠ¢ OpenAddSubgroup R
  refine âŸ¨(I ^ n).toAddSubgroup, ?_âŸ©
  -- âŠ¢ IsOpen (toAddSubgroup (I ^ n)).toAddSubmonoid.toAddSubsemigroup.carrier
  convert (I.adic_basis.toRing_subgroups_basis.openAddSubgroup n).isOpen
  -- âŠ¢ (toAddSubgroup (I ^ n)).toAddSubmonoid.toAddSubsemigroup.carrier = â†‘(RingSub â€¦
  change (I ^ n : Set R) = (I ^ n â€¢ (âŠ¤ : Ideal R) : Set R)
  -- âŠ¢ â†‘(I ^ n) = â†‘(I ^ n â€¢ âŠ¤)
  simp [smul_top_eq_map, Algebra.id.map_eq_id, map_id, restrictScalars_self]
  -- ğŸ‰ no goals
#align ideal.open_add_subgroup Ideal.openAddSubgroup

end Ideal

section IsAdic

/-- Given a topology on a ring `R` and an ideal `J`, `IsAdic J` means the topology is the
`J`-adic one. -/
def IsAdic [H : TopologicalSpace R] (J : Ideal R) : Prop :=
  H = J.adicTopology
#align is_adic IsAdic

/-- A topological ring is `J`-adic if and only if it admits the powers of `J` as a basis of
open neighborhoods of zero. -/
theorem isAdic_iff [top : TopologicalSpace R] [TopologicalRing R] {J : Ideal R} :
    IsAdic J â†”
      (âˆ€ n : â„•, IsOpen ((J ^ n : Ideal R) : Set R)) âˆ§
        âˆ€ s âˆˆ ğ“ (0 : R), âˆƒ n : â„•, ((J ^ n : Ideal R) : Set R) âŠ† s := by
  constructor
  -- âŠ¢ IsAdic J â†’ (âˆ€ (n : â„•), IsOpen â†‘(J ^ n)) âˆ§ âˆ€ (s : Set R), s âˆˆ ğ“ 0 â†’ âˆƒ n, â†‘(J  â€¦
  Â· intro H
    -- âŠ¢ (âˆ€ (n : â„•), IsOpen â†‘(J ^ n)) âˆ§ âˆ€ (s : Set R), s âˆˆ ğ“ 0 â†’ âˆƒ n, â†‘(J ^ n) âŠ† s
    change _ = _ at H
    -- âŠ¢ (âˆ€ (n : â„•), IsOpen â†‘(J ^ n)) âˆ§ âˆ€ (s : Set R), s âˆˆ ğ“ 0 â†’ âˆƒ n, â†‘(J ^ n) âŠ† s
    rw [H]
    -- âŠ¢ (âˆ€ (n : â„•), IsOpen â†‘(J ^ n)) âˆ§ âˆ€ (s : Set R), s âˆˆ ğ“ 0 â†’ âˆƒ n, â†‘(J ^ n) âŠ† s
    letI := J.adicTopology
    -- âŠ¢ (âˆ€ (n : â„•), IsOpen â†‘(J ^ n)) âˆ§ âˆ€ (s : Set R), s âˆˆ ğ“ 0 â†’ âˆƒ n, â†‘(J ^ n) âŠ† s
    constructor
    -- âŠ¢ âˆ€ (n : â„•), IsOpen â†‘(J ^ n)
    Â· intro n
      -- âŠ¢ IsOpen â†‘(J ^ n)
      exact (J.openAddSubgroup n).isOpen'
      -- ğŸ‰ no goals
    Â· intro s hs
      -- âŠ¢ âˆƒ n, â†‘(J ^ n) âŠ† s
      simpa using J.hasBasis_nhds_zero_adic.mem_iff.mp hs
      -- ğŸ‰ no goals
  Â· rintro âŸ¨Hâ‚, Hâ‚‚âŸ©
    -- âŠ¢ IsAdic J
    apply TopologicalAddGroup.ext
    Â· apply @TopologicalRing.to_topologicalAddGroup
      -- ğŸ‰ no goals
    Â· apply (RingSubgroupsBasis.toRingFilterBasis _).toAddGroupFilterBasis.isTopologicalAddGroup
      -- ğŸ‰ no goals
    Â· ext s
      -- âŠ¢ s âˆˆ ğ“ 0 â†” s âˆˆ ğ“ 0
      letI := Ideal.adic_basis J
      -- âŠ¢ s âˆˆ ğ“ 0 â†” s âˆˆ ğ“ 0
      rw [J.hasBasis_nhds_zero_adic.mem_iff]
      -- âŠ¢ s âˆˆ ğ“ 0 â†” âˆƒ i, True âˆ§ â†‘(J ^ i) âŠ† s
      constructor <;> intro H
      -- âŠ¢ s âˆˆ ğ“ 0 â†’ âˆƒ i, True âˆ§ â†‘(J ^ i) âŠ† s
                      -- âŠ¢ âˆƒ i, True âˆ§ â†‘(J ^ i) âŠ† s
                      -- âŠ¢ s âˆˆ ğ“ 0
      Â· rcases Hâ‚‚ s H with âŸ¨n, hâŸ©
        -- âŠ¢ âˆƒ i, True âˆ§ â†‘(J ^ i) âŠ† s
        exact âŸ¨n, trivial, hâŸ©
        -- ğŸ‰ no goals
      Â· rcases H with âŸ¨n, -, hnâŸ©
        -- âŠ¢ s âˆˆ ğ“ 0
        rw [mem_nhds_iff]
        -- âŠ¢ âˆƒ t, t âŠ† s âˆ§ IsOpen t âˆ§ 0 âˆˆ t
        refine' âŸ¨_, hn, Hâ‚ n, (J ^ n).zero_memâŸ©
        -- ğŸ‰ no goals
#align is_adic_iff isAdic_iff

variable [TopologicalSpace R] [TopologicalRing R]

theorem is_ideal_adic_pow {J : Ideal R} (h : IsAdic J) {n : â„•} (hn : 0 < n) : IsAdic (J ^ n) := by
  rw [isAdic_iff] at h âŠ¢
  -- âŠ¢ (âˆ€ (n_1 : â„•), IsOpen â†‘((J ^ n) ^ n_1)) âˆ§ âˆ€ (s : Set R), s âˆˆ ğ“ 0 â†’ âˆƒ n_1, â†‘(( â€¦
  constructor
  -- âŠ¢ âˆ€ (n_1 : â„•), IsOpen â†‘((J ^ n) ^ n_1)
  Â· intro m
    -- âŠ¢ IsOpen â†‘((J ^ n) ^ m)
    rw [â† pow_mul]
    -- âŠ¢ IsOpen â†‘(J ^ (n * m))
    apply h.left
    -- ğŸ‰ no goals
  Â· intro V hV
    -- âŠ¢ âˆƒ n_1, â†‘((J ^ n) ^ n_1) âŠ† V
    cases' h.right V hV with m hm
    -- âŠ¢ âˆƒ n_1, â†‘((J ^ n) ^ n_1) âŠ† V
    use m
    -- âŠ¢ â†‘((J ^ n) ^ m) âŠ† V
    refine' Set.Subset.trans _ hm
    -- âŠ¢ â†‘((J ^ n) ^ m) âŠ† â†‘(J ^ m)
    cases n
    -- âŠ¢ â†‘((J ^ Nat.zero) ^ m) âŠ† â†‘(J ^ m)
    Â· exfalso
      -- âŠ¢ False
      exact Nat.not_succ_le_zero 0 hn
      -- ğŸ‰ no goals
    rw [â† pow_mul, Nat.succ_mul]
    -- âŠ¢ â†‘(J ^ (nâœ * m + m)) âŠ† â†‘(J ^ m)
    apply Ideal.pow_le_pow
    -- âŠ¢ m â‰¤ nâœ * m + m
    apply Nat.le_add_left
    -- ğŸ‰ no goals
#align is_ideal_adic_pow is_ideal_adic_pow

theorem is_bot_adic_iff {A : Type*} [CommRing A] [TopologicalSpace A] [TopologicalRing A] :
    IsAdic (âŠ¥ : Ideal A) â†” DiscreteTopology A := by
  rw [isAdic_iff]
  -- âŠ¢ ((âˆ€ (n : â„•), IsOpen â†‘(âŠ¥ ^ n)) âˆ§ âˆ€ (s : Set A), s âˆˆ ğ“ 0 â†’ âˆƒ n, â†‘(âŠ¥ ^ n) âŠ† s)  â€¦
  constructor
  -- âŠ¢ ((âˆ€ (n : â„•), IsOpen â†‘(âŠ¥ ^ n)) âˆ§ âˆ€ (s : Set A), s âˆˆ ğ“ 0 â†’ âˆƒ n, â†‘(âŠ¥ ^ n) âŠ† s)  â€¦
  Â· rintro âŸ¨h, _h'âŸ©
    -- âŠ¢ DiscreteTopology A
    rw [discreteTopology_iff_open_singleton_zero]
    -- âŠ¢ IsOpen {0}
    simpa using h 1
    -- ğŸ‰ no goals
  Â· intros
    -- âŠ¢ (âˆ€ (n : â„•), IsOpen â†‘(âŠ¥ ^ n)) âˆ§ âˆ€ (s : Set A), s âˆˆ ğ“ 0 â†’ âˆƒ n, â†‘(âŠ¥ ^ n) âŠ† s
    constructor
    -- âŠ¢ âˆ€ (n : â„•), IsOpen â†‘(âŠ¥ ^ n)
    Â· simp
      -- ğŸ‰ no goals
    Â· intro U U_nhds
      -- âŠ¢ âˆƒ n, â†‘(âŠ¥ ^ n) âŠ† U
      use 1
      -- âŠ¢ â†‘(âŠ¥ ^ 1) âŠ† U
      simp [mem_of_mem_nhds U_nhds]
      -- ğŸ‰ no goals
#align is_bot_adic_iff is_bot_adic_iff

end IsAdic

/-- The ring `R` is equipped with a preferred ideal. -/
class WithIdeal (R : Type*) [CommRing R] where
  i : Ideal R
#align with_ideal WithIdeal

namespace WithIdeal

variable (R)

variable [WithIdeal R]

instance (priority := 100) : TopologicalSpace R :=
  i.adicTopology

instance (priority := 100) : NonarchimedeanRing R :=
  RingSubgroupsBasis.nonarchimedean _

instance (priority := 100) : UniformSpace R :=
  TopologicalAddGroup.toUniformSpace R

instance (priority := 100) : UniformAddGroup R :=
  comm_topologicalAddGroup_is_uniform

/-- The adic topology on an `R` module coming from the ideal `WithIdeal.I`.
This cannot be an instance because `R` cannot be inferred from `M`. -/
def topologicalSpaceModule (M : Type*) [AddCommGroup M] [Module R M] : TopologicalSpace M :=
  (i : Ideal R).adicModuleTopology M
#align with_ideal.topological_space_module WithIdeal.topologicalSpaceModule

/-
The next examples are kept to make sure potential future refactors won't break the instance
chaining.
-/
example : NonarchimedeanRing R := by infer_instance
                                     -- ğŸ‰ no goals

example : TopologicalRing (UniformSpace.Completion R) := by infer_instance
                                                            -- ğŸ‰ no goals

example (M : Type*) [AddCommGroup M] [Module R M] :
    @TopologicalAddGroup M (WithIdeal.topologicalSpaceModule R M) _ := by infer_instance
                                                                          -- ğŸ‰ no goals

example (M : Type*) [AddCommGroup M] [Module R M] :
    @ContinuousSMul R M _ _ (WithIdeal.topologicalSpaceModule R M) := by infer_instance
                                                                         -- ğŸ‰ no goals

example (M : Type*) [AddCommGroup M] [Module R M] :
    @NonarchimedeanAddGroup M _ (WithIdeal.topologicalSpaceModule R M) :=
  SubmodulesBasis.nonarchimedean _

end WithIdeal
