/-
Copyright (c) 2021 Ashwin Iyengar. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, Johan Commelin, Ashwin Iyengar, Patrick Massot
-/
import Mathlib.Algebra.Group.Subgroup.Basic
import Mathlib.Topology.Algebra.OpenSubgroup
import Mathlib.Topology.Algebra.Ring.Basic

/-!
# Nonarchimedean Topology

In this file we set up the theory of nonarchimedean topological groups and rings.

A nonarchimedean group is a topological group whose topology admits a basis of
open neighborhoods of the identity element in the group consisting of open subgroups.
A nonarchimedean ring is a topological ring whose underlying topological (additive)
group is nonarchimedean.

## Definitions

- `NonarchimedeanAddGroup`: nonarchimedean additive group.
- `NonarchimedeanGroup`: nonarchimedean multiplicative group.
- `NonarchimedeanRing`: nonarchimedean ring.

-/

open Topology
open scoped Pointwise

/-- A topological additive group is nonarchimedean if every neighborhood of 0
  contains an open subgroup. -/
class NonarchimedeanAddGroup (G : Type*) [AddGroup G] [TopologicalSpace G] : Prop
  extends IsTopologicalAddGroup G where
  is_nonarchimedean : ‚àÄ U ‚àà ùìù (0 : G), ‚àÉ V : OpenAddSubgroup G, (V : Set G) ‚äÜ U

/-- A topological group is nonarchimedean if every neighborhood of 1 contains an open subgroup. -/
@[to_additive]
class NonarchimedeanGroup (G : Type*) [Group G] [TopologicalSpace G] : Prop
  extends IsTopologicalGroup G where
  is_nonarchimedean : ‚àÄ U ‚àà ùìù (1 : G), ‚àÉ V : OpenSubgroup G, (V : Set G) ‚äÜ U

/-- A topological ring is nonarchimedean if its underlying topological additive
  group is nonarchimedean. -/
class NonarchimedeanRing (R : Type*) [Ring R] [TopologicalSpace R] : Prop
  extends IsTopologicalRing R where
  is_nonarchimedean : ‚àÄ U ‚àà ùìù (0 : R), ‚àÉ V : OpenAddSubgroup R, (V : Set R) ‚äÜ U

-- see Note [lower instance priority]
/-- Every nonarchimedean ring is naturally a nonarchimedean additive group. -/
instance (priority := 100) NonarchimedeanRing.to_nonarchimedeanAddGroup (R : Type*) [Ring R]
    [TopologicalSpace R] [t : NonarchimedeanRing R] : NonarchimedeanAddGroup R :=
  { t with }

namespace NonarchimedeanGroup

variable {G : Type*} [Group G] [TopologicalSpace G] [NonarchimedeanGroup G]
variable {H : Type*} [Group H] [TopologicalSpace H] [IsTopologicalGroup H]
variable {K : Type*} [Group K] [TopologicalSpace K] [NonarchimedeanGroup K]

/-- If a topological group embeds into a nonarchimedean group, then it is nonarchimedean. -/
@[to_additive]
theorem nonarchimedean_of_emb (f : G ‚Üí* H) (emb : IsOpenEmbedding f) : NonarchimedeanGroup H :=
  { is_nonarchimedean := fun U hU =>
      have h‚ÇÅ : f ‚Åª¬π' U ‚àà ùìù (1 : G) := by
        apply emb.continuous.tendsto
        rwa [f.map_one]
      let ‚ü®V, hV‚ü© := is_nonarchimedean (f ‚Åª¬π' U) h‚ÇÅ
      ‚ü®{ Subgroup.map f V with isOpen' := emb.isOpenMap _ V.isOpen }, Set.image_subset_iff.2 hV‚ü© }

/-- An open neighborhood of the identity in the cartesian product of two nonarchimedean groups
contains the cartesian product of an open neighborhood in each group. -/
@[to_additive NonarchimedeanAddGroup.prod_subset /-- An open neighborhood of the identity in
the cartesian product of two nonarchimedean groups contains the cartesian product of
an open neighborhood in each group. -/]
theorem prod_subset {U} (hU : U ‚àà ùìù (1 : G √ó K)) :
    ‚àÉ (V : OpenSubgroup G) (W : OpenSubgroup K), (V : Set G) √óÀ¢ (W : Set K) ‚äÜ U := by
  rw [nhds_prod_eq, Filter.mem_prod_iff] at hU
  rcases hU with ‚ü®U‚ÇÅ, hU‚ÇÅ, U‚ÇÇ, hU‚ÇÇ, h‚ü©
  obtain ‚ü®V, hV‚ü© := is_nonarchimedean _ hU‚ÇÅ
  obtain ‚ü®W, hW‚ü© := is_nonarchimedean _ hU‚ÇÇ
  use V
  grind

/-- An open neighborhood of the identity in the cartesian square of a nonarchimedean group
contains the cartesian square of an open neighborhood in the group. -/
@[to_additive NonarchimedeanAddGroup.prod_self_subset /-- An open neighborhood of the identity in
the cartesian square of a nonarchimedean group contains the cartesian square of
an open neighborhood in the group. -/]
theorem prod_self_subset {U} (hU : U ‚àà ùìù (1 : G √ó G)) :
    ‚àÉ V : OpenSubgroup G, (V : Set G) √óÀ¢ (V : Set G) ‚äÜ U :=
  let ‚ü®V, W, h‚ü© := prod_subset hU
  ‚ü®V ‚äì W, by refine Set.Subset.trans (Set.prod_mono ?_ ?_) ‚Äπ_‚Ä∫ <;> simp‚ü©

/-- The cartesian product of two nonarchimedean groups is nonarchimedean. -/
@[to_additive /-- The cartesian product of two nonarchimedean groups is nonarchimedean. -/]
instance Prod.instNonarchimedeanGroup : NonarchimedeanGroup (G √ó K) where
  is_nonarchimedean _ hU :=
    let ‚ü®V, W, h‚ü© := prod_subset hU
    ‚ü®V.prod W, ‚Äπ_‚Ä∫‚ü©

end NonarchimedeanGroup

namespace NonarchimedeanRing

open NonarchimedeanAddGroup

variable {R S : Type*}
variable [Ring R] [TopologicalSpace R] [NonarchimedeanRing R]
variable [Ring S] [TopologicalSpace S] [NonarchimedeanRing S]

/-- The cartesian product of two nonarchimedean rings is nonarchimedean. -/
instance : NonarchimedeanRing (R √ó S) where
  is_nonarchimedean := NonarchimedeanAddGroup.is_nonarchimedean

/-- Given an open subgroup `U` and an element `r` of a nonarchimedean ring, there is an open
  subgroup `V` such that `r ‚Ä¢ V` is contained in `U`. -/
theorem left_mul_subset (U : OpenAddSubgroup R) (r : R) :
    ‚àÉ V : OpenAddSubgroup R, r ‚Ä¢ (V : Set R) ‚äÜ U :=
  ‚ü®U.comap (AddMonoidHom.mulLeft r) (continuous_mul_left r), (U : Set R).image_preimage_subset _‚ü©

/-- An open subgroup of a nonarchimedean ring contains the square of another one. -/
theorem mul_subset (U : OpenAddSubgroup R) : ‚àÉ V : OpenAddSubgroup R, (V : Set R) * V ‚äÜ U := by
  let ‚ü®V, H‚ü© := prod_self_subset <| (U.isOpen.preimage continuous_mul).mem_nhds <| by
    simpa only [Set.mem_preimage, Prod.snd_zero, mul_zero] using U.zero_mem
  use V
  rintro v ‚ü®a, ha, b, hb, hv‚ü©
  have hy := H (Set.mk_mem_prod ha hb)
  simp only [Set.mem_preimage, SetLike.mem_coe, hv] at hy
  rw [SetLike.mem_coe]
  exact hy

end NonarchimedeanRing
