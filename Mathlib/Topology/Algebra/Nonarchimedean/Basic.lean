/-
Copyright (c) 2021 Ashwin Iyengar. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, Johan Commelin, Ashwin Iyengar, Patrick Massot
-/
import Mathlib.GroupTheory.Subgroup.Basic
import Mathlib.Topology.Algebra.OpenSubgroup
import Mathlib.Topology.Algebra.Ring.Basic

#align_import topology.algebra.nonarchimedean.basic from "leanprover-community/mathlib"@"83f81aea33931a1edb94ce0f32b9a5d484de6978"

/-!
# Nonarchimedean Topology

In this file we set up the theory of nonarchimedean topological groups and rings.

A nonarchimedean group is a topological group whose topology admits a basis of
open neighborhoods of the identity element in the group consisting of open subgroups.
A nonarchimedean ring is a topological ring whose underlying topological (additive)
group is nonarchimedean.

## Definitions

- `NonarchimedeanAddGroup`: nonarchimedean additive group.
- `NonarchimedeanGroup`: nonarchimedean multiplicative group.
- `NonarchimedeanRing`: nonarchimedean ring.

-/


open Pointwise

/-- A topological additive group is nonarchimedean if every neighborhood of 0
  contains an open subgroup. -/
class NonarchimedeanAddGroup (G : Type*) [AddGroup G] [TopologicalSpace G] extends
  TopologicalAddGroup G : Prop where
  is_nonarchimedean : âˆ€ U âˆˆ nhds (0 : G), âˆƒ V : OpenAddSubgroup G, (V : Set G) âŠ† U
#align nonarchimedean_add_group NonarchimedeanAddGroup

/-- A topological group is nonarchimedean if every neighborhood of 1 contains an open subgroup. -/
@[to_additive]
class NonarchimedeanGroup (G : Type*) [Group G] [TopologicalSpace G] extends TopologicalGroup G :
  Prop where
  is_nonarchimedean : âˆ€ U âˆˆ nhds (1 : G), âˆƒ V : OpenSubgroup G, (V : Set G) âŠ† U
#align nonarchimedean_group NonarchimedeanGroup

/-- A topological ring is nonarchimedean if its underlying topological additive
  group is nonarchimedean. -/
class NonarchimedeanRing (R : Type*) [Ring R] [TopologicalSpace R] extends TopologicalRing R :
  Prop where
  is_nonarchimedean : âˆ€ U âˆˆ nhds (0 : R), âˆƒ V : OpenAddSubgroup R, (V : Set R) âŠ† U
#align nonarchimedean_ring NonarchimedeanRing

-- see Note [lower instance priority]
/-- Every nonarchimedean ring is naturally a nonarchimedean additive group. -/
instance (priority := 100) NonarchimedeanRing.to_nonarchimedeanAddGroup (R : Type*) [Ring R]
    [TopologicalSpace R] [t : NonarchimedeanRing R] : NonarchimedeanAddGroup R :=
  { t with }
#align nonarchimedean_ring.to_nonarchimedean_add_group NonarchimedeanRing.to_nonarchimedeanAddGroup

namespace NonarchimedeanGroup

variable {G : Type*} [Group G] [TopologicalSpace G] [NonarchimedeanGroup G]

variable {H : Type*} [Group H] [TopologicalSpace H] [TopologicalGroup H]

variable {K : Type*} [Group K] [TopologicalSpace K] [NonarchimedeanGroup K]

/-- If a topological group embeds into a nonarchimedean group, then it is nonarchimedean. -/
@[to_additive]
theorem nonarchimedean_of_emb (f : G â†’* H) (emb : OpenEmbedding f) : NonarchimedeanGroup H :=
  { is_nonarchimedean := fun U hU =>
      have hâ‚ : f â»Â¹' U âˆˆ nhds (1 : G) := by
        apply emb.continuous.tendsto
        -- âŠ¢ U âˆˆ nhds (â†‘f 1)
        rwa [f.map_one]
        -- ğŸ‰ no goals
      let âŸ¨V, hVâŸ© := is_nonarchimedean (f â»Â¹' U) hâ‚
      âŸ¨{ Subgroup.map f V with isOpen' := emb.isOpenMap _ V.isOpen }, Set.image_subset_iff.2 hVâŸ© }
#align nonarchimedean_group.nonarchimedean_of_emb NonarchimedeanGroup.nonarchimedean_of_emb
#align nonarchimedean_add_group.nonarchimedean_of_emb NonarchimedeanAddGroup.nonarchimedean_of_emb

/-- An open neighborhood of the identity in the cartesian product of two nonarchimedean groups
contains the cartesian product of an open neighborhood in each group. -/
@[to_additive NonarchimedeanAddGroup.prod_subset "An open neighborhood of the identity in
the cartesian product of two nonarchimedean groups contains the cartesian product of
an open neighborhood in each group."]
theorem prod_subset {U} (hU : U âˆˆ nhds (1 : G Ã— K)) :
    âˆƒ (V : OpenSubgroup G) (W : OpenSubgroup K), (V : Set G) Ã—Ë¢ (W : Set K) âŠ† U := by
  erw [nhds_prod_eq, Filter.mem_prod_iff] at hU
  -- âŠ¢ âˆƒ V W, â†‘V Ã—Ë¢ â†‘W âŠ† U
  rcases hU with âŸ¨Uâ‚, hUâ‚, Uâ‚‚, hUâ‚‚, hâŸ©
  -- âŠ¢ âˆƒ V W, â†‘V Ã—Ë¢ â†‘W âŠ† U
  cases' is_nonarchimedean _ hUâ‚ with V hV
  -- âŠ¢ âˆƒ V W, â†‘V Ã—Ë¢ â†‘W âŠ† U
  cases' is_nonarchimedean _ hUâ‚‚ with W hW
  -- âŠ¢ âˆƒ V W, â†‘V Ã—Ë¢ â†‘W âŠ† U
  use V; use W
  -- âŠ¢ âˆƒ W, â†‘V Ã—Ë¢ â†‘W âŠ† U
         -- âŠ¢ â†‘V Ã—Ë¢ â†‘W âŠ† U
  rw [Set.prod_subset_iff]
  -- âŠ¢ âˆ€ (x : G), x âˆˆ â†‘V â†’ âˆ€ (y : K), y âˆˆ â†‘W â†’ (x, y) âˆˆ U
  intro x hX y hY
  -- âŠ¢ (x, y) âˆˆ U
  exact Set.Subset.trans (Set.prod_mono hV hW) h (Set.mem_sep hX hY)
  -- ğŸ‰ no goals
#align nonarchimedean_group.prod_subset NonarchimedeanGroup.prod_subset
#align nonarchimedean_add_group.prod_subset NonarchimedeanAddGroup.prod_subset

/-- An open neighborhood of the identity in the cartesian square of a nonarchimedean group
contains the cartesian square of an open neighborhood in the group. -/
@[to_additive NonarchimedeanAddGroup.prod_self_subset "An open neighborhood of the identity in
the cartesian square of a nonarchimedean group contains the cartesian square of
an open neighborhood in the group."]
theorem prod_self_subset {U} (hU : U âˆˆ nhds (1 : G Ã— G)) :
    âˆƒ V : OpenSubgroup G, (V : Set G) Ã—Ë¢ (V : Set G) âŠ† U :=
  let âŸ¨V, W, hâŸ© := prod_subset hU
  âŸ¨V âŠ“ W, by refine' Set.Subset.trans (Set.prod_mono _ _) â€¹_â€º <;> simpâŸ©
             -- âŠ¢ â†‘(V âŠ“ W) âŠ† â†‘V
                                                                  -- ğŸ‰ no goals
                                                                  -- ğŸ‰ no goals
#align nonarchimedean_group.prod_self_subset NonarchimedeanGroup.prod_self_subset
#align nonarchimedean_add_group.prod_self_subset NonarchimedeanAddGroup.prod_self_subset

/-- The cartesian product of two nonarchimedean groups is nonarchimedean. -/
@[to_additive "The cartesian product of two nonarchimedean groups is nonarchimedean."]
instance : NonarchimedeanGroup (G Ã— K)
    where is_nonarchimedean U hU :=
    let âŸ¨V, W, hâŸ© := prod_subset hU
    âŸ¨V.prod W, â€¹_â€ºâŸ©

end NonarchimedeanGroup

namespace NonarchimedeanRing

open NonarchimedeanRing

open NonarchimedeanAddGroup

variable {R S : Type*}

variable [Ring R] [TopologicalSpace R] [NonarchimedeanRing R]

variable [Ring S] [TopologicalSpace S] [NonarchimedeanRing S]

/-- The cartesian product of two nonarchimedean rings is nonarchimedean. -/
instance : NonarchimedeanRing (R Ã— S)
    where is_nonarchimedean := NonarchimedeanAddGroup.is_nonarchimedean

/-- Given an open subgroup `U` and an element `r` of a nonarchimedean ring, there is an open
  subgroup `V` such that `r â€¢ V` is contained in `U`. -/
theorem left_mul_subset (U : OpenAddSubgroup R) (r : R) :
    âˆƒ V : OpenAddSubgroup R, r â€¢ (V : Set R) âŠ† U :=
  âŸ¨U.comap (AddMonoidHom.mulLeft r) (continuous_mul_left r), (U : Set R).image_preimage_subset _âŸ©
#align nonarchimedean_ring.left_mul_subset NonarchimedeanRing.left_mul_subset

/-- An open subgroup of a nonarchimedean ring contains the square of another one. -/
theorem mul_subset (U : OpenAddSubgroup R) : âˆƒ V : OpenAddSubgroup R, (V : Set R) * V âŠ† U := by
  let âŸ¨V, HâŸ© :=
    prod_self_subset
      (IsOpen.mem_nhds (IsOpen.preimage continuous_mul U.isOpen)
        (by simpa only [Set.mem_preimage, SetLike.mem_coe, Prod.snd_zero,
            mul_zero] using U.zero_mem))
  use V
  -- âŠ¢ â†‘V * â†‘V âŠ† â†‘U
  rintro v âŸ¨a, b, ha, hb, hvâŸ©
  -- âŠ¢ v âˆˆ â†‘U
  have hy := H (Set.mk_mem_prod ha hb)
  -- âŠ¢ v âˆˆ â†‘U
  simp only [Set.mem_preimage, SetLike.mem_coe, hv] at hy
  -- âŠ¢ v âˆˆ â†‘U
  rw [SetLike.mem_coe]
  -- âŠ¢ v âˆˆ U
  exact hy
  -- ğŸ‰ no goals
#align nonarchimedean_ring.mul_subset NonarchimedeanRing.mul_subset

end NonarchimedeanRing
