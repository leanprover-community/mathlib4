/-
Copyright (c) 2021 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
import Mathlib.Algebra.Algebra.Basic
import Mathlib.Algebra.Module.Submodule.Pointwise
import Mathlib.Topology.Algebra.FilterBasis
import Mathlib.Topology.Algebra.Nonarchimedean.Basic

/-!
# Neighborhood bases for non-archimedean rings and modules

This files contains special families of filter bases on rings and modules that give rise to
non-archimedean topologies.

The main definition is `RingSubgroupsBasis` which is a predicate on a family of
additive subgroups of a ring. The predicate ensures there is a topology
`RingSubgroupsBasis.topology` which is compatible with a ring structure and admits the given
family as a basis of neighborhoods of zero. In particular the given subgroups become open subgroups
(bundled in `RingSubgroupsBasis.openAddSubgroup`) and we get a non-archimedean topological ring
(`RingSubgroupsBasis.nonarchimedean`).

A special case of this construction is given by `SubmodulesBasis` where the subgroups are
sub-modules in a commutative algebra. This important example gives rise to the adic topology
(studied in its own file).
-/

open Set Filter Function Lattice

open Topology Filter Pointwise

/-- A family of additive subgroups on a ring `A` is a subgroups basis if it satisfies some
axioms ensuring there is a topology on `A` which is compatible with the ring structure and
admits this family as a basis of neighborhoods of zero. -/
structure RingSubgroupsBasis {A Œπ : Type*} [Ring A] (B : Œπ ‚Üí AddSubgroup A) : Prop where
  /-- Condition for `B` to be a filter basis on `A`. -/
  inter : ‚àÄ i j, ‚àÉ k, B k ‚â§ B i ‚äì B j
  /-- For each set `B` in the submodule basis on `A`, there is another basis element `B'` such
  that the set-theoretic product `B' * B'` is in `B`. -/
  mul : ‚àÄ i, ‚àÉ j, (B j : Set A) * B j ‚äÜ B i
  /-- For any element `x : A` and any set `B` in the submodule basis on `A`,
  there is another basis element `B'` such that `B' * x` is in `B`. -/
  leftMul : ‚àÄ x : A, ‚àÄ i, ‚àÉ j, (B j : Set A) ‚äÜ (x * ¬∑) ‚Åª¬π' B i
  /-- For any element `x : A` and any set `B` in the submodule basis on `A`,
  there is another basis element `B'` such that `x * B'` is in `B`. -/
  rightMul : ‚àÄ x : A, ‚àÄ i, ‚àÉ j, (B j : Set A) ‚äÜ (¬∑ * x) ‚Åª¬π' B i

namespace RingSubgroupsBasis

variable {A Œπ : Type*} [Ring A]

theorem of_comm {A Œπ : Type*} [CommRing A] (B : Œπ ‚Üí AddSubgroup A)
    (inter : ‚àÄ i j, ‚àÉ k, B k ‚â§ B i ‚äì B j) (mul : ‚àÄ i, ‚àÉ j, (B j : Set A) * B j ‚äÜ B i)
    (leftMul : ‚àÄ x : A, ‚àÄ i, ‚àÉ j, (B j : Set A) ‚äÜ (fun y : A => x * y) ‚Åª¬π' B i) :
    RingSubgroupsBasis B :=
  { inter
    mul
    leftMul
    rightMul := fun x i ‚Ü¶ (leftMul x i).imp fun j hj ‚Ü¶ by simpa only [mul_comm] using hj }

/-- Every subgroups basis on a ring leads to a ring filter basis. -/
def toRingFilterBasis [Nonempty Œπ] {B : Œπ ‚Üí AddSubgroup A} (hB : RingSubgroupsBasis B) :
    RingFilterBasis A where
  sets := { U | ‚àÉ i, U = B i }
  nonempty := by
    inhabit Œπ
    exact ‚ü®B default, default, rfl‚ü©
  inter_sets := by
    rintro _ _ ‚ü®i, rfl‚ü© ‚ü®j, rfl‚ü©
    obtain ‚ü®k, hk‚ü© := hB.inter i j
    use B k
    constructor
    ¬∑ use k
    ¬∑ exact hk
  zero' := by
    rintro _ ‚ü®i, rfl‚ü©
    exact (B i).zero_mem
  add' := by
    rintro _ ‚ü®i, rfl‚ü©
    use B i
    constructor
    ¬∑ use i
    ¬∑ rintro x ‚ü®y, y_in, z, z_in, rfl‚ü©
      exact (B i).add_mem y_in z_in
  neg' := by
    rintro _ ‚ü®i, rfl‚ü©
    use B i
    constructor
    ¬∑ use i
    ¬∑ intro x x_in
      exact (B i).neg_mem x_in
  conj' := by
    rintro x‚ÇÄ _ ‚ü®i, rfl‚ü©
    use B i
    constructor
    ¬∑ use i
    ¬∑ simp
  mul' := by
    rintro _ ‚ü®i, rfl‚ü©
    obtain ‚ü®k, hk‚ü© := hB.mul i
    use B k
    constructor
    ¬∑ use k
    ¬∑ exact hk
  mul_left' := by
    rintro x‚ÇÄ _ ‚ü®i, rfl‚ü©
    obtain ‚ü®k, hk‚ü© := hB.leftMul x‚ÇÄ i
    use B k
    constructor
    ¬∑ use k
    ¬∑ exact hk
  mul_right' := by
    rintro x‚ÇÄ _ ‚ü®i, rfl‚ü©
    obtain ‚ü®k, hk‚ü© := hB.rightMul x‚ÇÄ i
    use B k
    constructor
    ¬∑ use k
    ¬∑ exact hk

variable [Nonempty Œπ] {B : Œπ ‚Üí AddSubgroup A} (hB : RingSubgroupsBasis B)

theorem mem_addGroupFilterBasis_iff {V : Set A} :
    V ‚àà hB.toRingFilterBasis.toAddGroupFilterBasis ‚Üî ‚àÉ i, V = B i :=
  Iff.rfl

theorem mem_addGroupFilterBasis (i) : (B i : Set A) ‚àà hB.toRingFilterBasis.toAddGroupFilterBasis :=
  ‚ü®i, rfl‚ü©

/-- The topology defined from a subgroups basis, admitting the given subgroups as a basis
of neighborhoods of zero. -/
def topology : TopologicalSpace A :=
  hB.toRingFilterBasis.toAddGroupFilterBasis.topology

theorem hasBasis_nhds_zero : HasBasis (@nhds A hB.topology 0) (fun _ => True) fun i => B i :=
  ‚ü®by
    intro s
    rw [hB.toRingFilterBasis.toAddGroupFilterBasis.nhds_zero_hasBasis.mem_iff]
    constructor
    ¬∑ rintro ‚ü®-, ‚ü®i, rfl‚ü©, hi‚ü©
      exact ‚ü®i, trivial, hi‚ü©
    ¬∑ rintro ‚ü®i, -, hi‚ü©
      exact ‚ü®B i, ‚ü®i, rfl‚ü©, hi‚ü©‚ü©

theorem hasBasis_nhds (a : A) :
    HasBasis (@nhds A hB.topology a) (fun _ => True) fun i => { b | b - a ‚àà B i } :=
  ‚ü®by
    intro s
    rw [(hB.toRingFilterBasis.toAddGroupFilterBasis.nhds_hasBasis a).mem_iff]
    simp only [true_and]
    constructor
    ¬∑ rintro ‚ü®-, ‚ü®i, rfl‚ü©, hi‚ü©
      use i
      suffices h : { b : A | b - a ‚àà B i } = (fun y => a + y) '' ‚Üë(B i) by
        rw [h]
        assumption
      simp only [image_add_left, neg_add_eq_sub]
      ext b
      simp
    ¬∑ rintro ‚ü®i, hi‚ü©
      use B i
      constructor
      ¬∑ use i
      ¬∑ rw [image_subset_iff]
        rintro b b_in
        apply hi
        simpa using b_in‚ü©

/-- Given a subgroups basis, the basis elements as open additive subgroups in the associated
topology. -/
def openAddSubgroup (i : Œπ) : @OpenAddSubgroup A _ hB.topology :=
  let _ := hB.topology
  { B i with
    isOpen' := by
      rw [isOpen_iff_mem_nhds]
      intro a a_in
      rw [(hB.hasBasis_nhds a).mem_iff]
      use i, trivial
      rintro b b_in
      simpa using (B i).add_mem a_in b_in }

-- See note [non-Archimedean non-instances]
theorem nonarchimedean : @NonarchimedeanRing A _ hB.topology := by
  letI := hB.topology
  constructor
  intro U hU
  obtain ‚ü®i, -, hi : (B i : Set A) ‚äÜ U‚ü© := hB.hasBasis_nhds_zero.mem_iff.mp hU
  exact ‚ü®hB.openAddSubgroup i, hi‚ü©

end RingSubgroupsBasis

variable {Œπ R A : Type*} [CommRing R] [CommRing A] [Algebra R A]

/-- A family of submodules in a commutative `R`-algebra `A` is a submodules basis if it satisfies
some axioms ensuring there is a topology on `A` which is compatible with the ring structure and
admits this family as a basis of neighborhoods of zero. -/
structure SubmodulesRingBasis (B : Œπ ‚Üí Submodule R A) : Prop where
  /-- Condition for `B` to be a filter basis on `A`. -/
  inter : ‚àÄ i j, ‚àÉ k, B k ‚â§ B i ‚äì B j
  /-- For any element `a : A` and any set `B` in the submodule basis on `A`,
  there is another basis element `B'` such that `a ‚Ä¢ B'` is in `B`. -/
  leftMul : ‚àÄ (a : A) (i), ‚àÉ j, a ‚Ä¢ B j ‚â§ B i
  /-- For each set `B` in the submodule basis on `A`, there is another basis element `B'` such
  that the set-theoretic product `B' * B'` is in `B`. -/
  mul : ‚àÄ i, ‚àÉ j, (B j : Set A) * B j ‚äÜ B i

namespace SubmodulesRingBasis

variable {B : Œπ ‚Üí Submodule R A} (hB : SubmodulesRingBasis B)

theorem toRing_subgroups_basis (hB : SubmodulesRingBasis B) :
    RingSubgroupsBasis fun i => (B i).toAddSubgroup := by
  apply RingSubgroupsBasis.of_comm (fun i => (B i).toAddSubgroup) hB.inter hB.mul
  intro a i
  rcases hB.leftMul a i with ‚ü®j, hj‚ü©
  use j
  rintro b (b_in : b ‚àà B j)
  exact hj ‚ü®b, b_in, rfl‚ü©

/-- The topology associated to a basis of submodules in an algebra. -/
def topology [Nonempty Œπ] (hB : SubmodulesRingBasis B) : TopologicalSpace A :=
  hB.toRing_subgroups_basis.topology

end SubmodulesRingBasis

variable {M : Type*} [AddCommGroup M] [Module R M]

/-- A family of submodules in an `R`-module `M` is a submodules basis if it satisfies
some axioms ensuring there is a topology on `M` which is compatible with the module structure and
admits this family as a basis of neighborhoods of zero. -/
structure SubmodulesBasis [TopologicalSpace R] (B : Œπ ‚Üí Submodule R M) : Prop where
  /-- Condition for `B` to be a filter basis on `M`. -/
  inter : ‚àÄ i j, ‚àÉ k, B k ‚â§ B i ‚äì B j
  /-- For any element `m : M` and any set `B` in the basis, `a ‚Ä¢ m` lies in `B` for all
  `a` sufficiently close to `0`. -/
  smul : ‚àÄ (m : M) (i : Œπ), ‚àÄ·∂† a in ùìù (0 : R), a ‚Ä¢ m ‚àà B i

namespace SubmodulesBasis

variable [TopologicalSpace R] [Nonempty Œπ] {B : Œπ ‚Üí Submodule R M} (hB : SubmodulesBasis B)

/-- The image of a submodules basis is a module filter basis. -/
def toModuleFilterBasis : ModuleFilterBasis R M where
  sets := { U | ‚àÉ i, U = B i }
  nonempty := by
    inhabit Œπ
    exact ‚ü®B default, default, rfl‚ü©
  inter_sets := by
    rintro _ _ ‚ü®i, rfl‚ü© ‚ü®j, rfl‚ü©
    obtain ‚ü®k, hk‚ü© := hB.inter i j
    use B k
    constructor
    ¬∑ use k
    ¬∑ exact hk
  zero' := by
    rintro _ ‚ü®i, rfl‚ü©
    exact (B i).zero_mem
  add' := by
    rintro _ ‚ü®i, rfl‚ü©
    use B i
    constructor
    ¬∑ use i
    ¬∑ rintro x ‚ü®y, y_in, z, z_in, rfl‚ü©
      exact (B i).add_mem y_in z_in
  neg' := by
    rintro _ ‚ü®i, rfl‚ü©
    use B i
    constructor
    ¬∑ use i
    ¬∑ intro x x_in
      exact (B i).neg_mem x_in
  conj' := by
    rintro x‚ÇÄ _ ‚ü®i, rfl‚ü©
    use B i
    constructor
    ¬∑ use i
    ¬∑ simp
  smul' := by
    rintro _ ‚ü®i, rfl‚ü©
    use univ
    constructor
    ¬∑ exact univ_mem
    ¬∑ use B i
      constructor
      ¬∑ use i
      ¬∑ rintro _ ‚ü®a, -, m, hm, rfl‚ü©
        exact (B i).smul_mem _ hm
  smul_left' := by
    rintro x‚ÇÄ _ ‚ü®i, rfl‚ü©
    use B i
    constructor
    ¬∑ use i
    ¬∑ intro m
      exact (B i).smul_mem _
  smul_right' := by
    rintro m‚ÇÄ _ ‚ü®i, rfl‚ü©
    exact hB.smul m‚ÇÄ i

/-- The topology associated to a basis of submodules in a module. -/
def topology : TopologicalSpace M :=
  hB.toModuleFilterBasis.toAddGroupFilterBasis.topology

/-- Given a submodules basis, the basis elements as open additive subgroups in the associated
topology. -/
def openAddSubgroup (i : Œπ) : @OpenAddSubgroup M _ hB.topology :=
  let _ := hB.topology
  { (B i).toAddSubgroup with
    isOpen' := by
      letI := hB.topology
      rw [isOpen_iff_mem_nhds]
      intro a a_in
      rw [(hB.toModuleFilterBasis.toAddGroupFilterBasis.nhds_hasBasis a).mem_iff]
      use B i
      constructor
      ¬∑ use i
      ¬∑ rintro - ‚ü®b, b_in, rfl‚ü©
        exact (B i).add_mem a_in b_in }

-- See note [non-Archimedean non-instances]
theorem nonarchimedean (hB : SubmodulesBasis B) : @NonarchimedeanAddGroup M _ hB.topology := by
  letI := hB.topology
  constructor
  intro U hU
  obtain ‚ü®-, ‚ü®i, rfl‚ü©, hi : (B i : Set M) ‚äÜ U‚ü© :=
    hB.toModuleFilterBasis.toAddGroupFilterBasis.nhds_zero_hasBasis.mem_iff.mp hU
  exact ‚ü®hB.openAddSubgroup i, hi‚ü©

library_note2 ¬´non-Archimedean non-instances¬ª /--
The non-Archimedean subgroup basis lemmas cannot be instances because some instances
(such as `MeasureTheory.AEEqFun.instAddMonoid` or `IsTopologicalAddGroup.toContinuousAdd`)
cause the search for `@IsTopologicalAddGroup Œ≤ ?m1 ?m2`, i.e. a search for a topological group where
the topology/group structure are unknown. -/


end SubmodulesBasis

section

/-
In this section, we check that in an `R`-algebra `A` over a ring equipped with a topology,
a basis of `R`-submodules which is compatible with the topology on `R` is also a submodule basis
in the sense of `R`-modules (forgetting about the ring structure on `A`) and those two points of
view definitionaly gives the same topology on `A`.
-/
variable [TopologicalSpace R] {B : Œπ ‚Üí Submodule R A} (hB : SubmodulesRingBasis B)
  (hsmul : ‚àÄ (m : A) (i : Œπ), ‚àÄ·∂† a : R in ùìù 0, a ‚Ä¢ m ‚àà B i)
include hB hsmul

theorem SubmodulesRingBasis.toSubmodulesBasis : SubmodulesBasis B :=
  { inter := hB.inter
    smul := hsmul }

example [Nonempty Œπ] : hB.topology = (hB.toSubmodulesBasis hsmul).topology :=
  rfl

end

/-- Given a ring filter basis on a commutative ring `R`, define a compatibility condition
on a family of submodules of an `R`-module `M`. This compatibility condition allows to get
a topological module structure. -/
structure RingFilterBasis.SubmodulesBasis (BR : RingFilterBasis R) (B : Œπ ‚Üí Submodule R M) :
    Prop where
  /-- Condition for `B` to be a filter basis on `M`. -/
  inter : ‚àÄ i j, ‚àÉ k, B k ‚â§ B i ‚äì B j
  /-- For any element `m : M` and any set `B i` in the submodule basis on `M`,
  there is a `U` in the ring filter basis on `R` such that `U * m` is in `B i`. -/
  smul : ‚àÄ (m : M) (i : Œπ), ‚àÉ U ‚àà BR, U ‚äÜ (¬∑ ‚Ä¢ m) ‚Åª¬π' B i

theorem RingFilterBasis.submodulesBasisIsBasis (BR : RingFilterBasis R) {B : Œπ ‚Üí Submodule R M}
    (hB : BR.SubmodulesBasis B) : @_root_.SubmodulesBasis Œπ R _ M _ _ BR.topology B :=
  let _ := BR.topology
  { inter := hB.inter
    smul := by
      letI := BR.topology
      intro m i
      rcases hB.smul m i with ‚ü®V, V_in, hV‚ü©
      exact mem_of_superset (BR.toAddGroupFilterBasis.mem_nhds_zero V_in) hV }

/-- The module filter basis associated to a ring filter basis and a compatible submodule basis.
This allows to build a topological module structure compatible with the given module structure
and the topology associated to the given ring filter basis. -/
def RingFilterBasis.moduleFilterBasis [Nonempty Œπ] (BR : RingFilterBasis R) {B : Œπ ‚Üí Submodule R M}
    (hB : BR.SubmodulesBasis B) : @ModuleFilterBasis R M _ BR.topology _ _ :=
  @SubmodulesBasis.toModuleFilterBasis Œπ R _ M _ _ BR.topology _ _ (BR.submodulesBasisIsBasis hB)
