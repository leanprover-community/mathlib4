/-
Copyright (c) 2018 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Johannes H√∂lzl
-/
import Mathlib.Algebra.Order.AbsoluteValue.Basic
import Mathlib.Algebra.Ring.Opposite
import Mathlib.Algebra.Ring.Prod
import Mathlib.Algebra.Ring.Subring.Basic
import Mathlib.Topology.Algebra.Group.GroupTopology

/-!

# Topological (semi)rings

A topological (semi)ring is a (semi)ring equipped with a topology such that all operations are
continuous. Besides this definition, this file proves that the topological closure of a subring
(resp. an ideal) is a subring (resp. an ideal) and defines products and quotients
of topological (semi)rings.

## Main Results

- `Subring.topologicalClosure`/`Subsemiring.topologicalClosure`: the topological closure of a
  `Subring`/`Subsemiring` is itself a `Sub(semi)ring`.
- The product of two topological (semi)rings is a topological (semi)ring.
- The indexed product of topological (semi)rings is a topological (semi)ring.
-/

assert_not_exists Cardinal

open Set Filter TopologicalSpace Function Topology Filter

section IsTopologicalSemiring

variable (Œ± : Type*)

/-- a topological semiring is a semiring `R` where addition and multiplication are continuous.
We allow for non-unital and non-associative semirings as well.

The `IsTopologicalSemiring` class should *only* be instantiated in the presence of a
`NonUnitalNonAssocSemiring` instance; if there is an instance of `NonUnitalNonAssocRing`,
then `IsTopologicalRing` should be used. Note: in the presence of `NonAssocRing`, these classes are
mathematically equivalent (see `IsTopologicalSemiring.continuousNeg_of_mul` or
`IsTopologicalSemiring.toIsTopologicalRing`). -/
class IsTopologicalSemiring [TopologicalSpace Œ±] [NonUnitalNonAssocSemiring Œ±] : Prop
    extends ContinuousAdd Œ±, ContinuousMul Œ±

@[deprecated (since := "2025-02-14")] alias TopologicalSemiring :=
  IsTopologicalSemiring

/-- A topological ring is a ring `R` where addition, multiplication and negation are continuous.

If `R` is a (unital) ring, then continuity of negation can be derived from continuity of
multiplication as it is multiplication with `-1`. (See
`IsTopologicalSemiring.continuousNeg_of_mul` and
`topological_semiring.to_topological_add_group`) -/
class IsTopologicalRing [TopologicalSpace Œ±] [NonUnitalNonAssocRing Œ±] : Prop
    extends IsTopologicalSemiring Œ±, ContinuousNeg Œ±

@[deprecated (since := "2025-02-14")] alias TopologicalRing :=
  IsTopologicalRing

variable {Œ±}

/-- If `R` is a ring with a continuous multiplication, then negation is continuous as well since it
is just multiplication with `-1`. -/
theorem IsTopologicalSemiring.continuousNeg_of_mul [TopologicalSpace Œ±] [NonAssocRing Œ±]
    [ContinuousMul Œ±] : ContinuousNeg Œ± where
  continuous_neg := by
    simpa using (continuous_const.mul continuous_id : Continuous fun x : Œ± => -1 * x)

/-- If `R` is a ring which is a topological semiring, then it is automatically a topological
ring. This exists so that one can place a topological ring structure on `R` without explicitly
proving `continuous_neg`. -/
theorem IsTopologicalSemiring.toIsTopologicalRing [TopologicalSpace Œ±] [NonAssocRing Œ±]
    (_ : IsTopologicalSemiring Œ±) : IsTopologicalRing Œ± where
  toContinuousNeg := IsTopologicalSemiring.continuousNeg_of_mul

@[deprecated (since := "2025-02-14")] alias TopologicalSemiring.toTopologicalRing :=
  IsTopologicalSemiring.toIsTopologicalRing

-- See note [lower instance priority]
instance (priority := 100) IsTopologicalRing.to_topologicalAddGroup [NonUnitalNonAssocRing Œ±]
    [TopologicalSpace Œ±] [IsTopologicalRing Œ±] : IsTopologicalAddGroup Œ± := ‚ü®‚ü©

instance (priority := 50) DiscreteTopology.topologicalSemiring [TopologicalSpace Œ±]
    [NonUnitalNonAssocSemiring Œ±] [DiscreteTopology Œ±] : IsTopologicalSemiring Œ± := ‚ü®‚ü©

instance (priority := 50) DiscreteTopology.topologicalRing [TopologicalSpace Œ±]
    [NonUnitalNonAssocRing Œ±] [DiscreteTopology Œ±] : IsTopologicalRing Œ± := ‚ü®‚ü©

section

namespace NonUnitalSubsemiring

variable [TopologicalSpace Œ±] [NonUnitalSemiring Œ±] [IsTopologicalSemiring Œ±]

instance instIsTopologicalSemiring (S : NonUnitalSubsemiring Œ±) : IsTopologicalSemiring S :=
  { S.toSubsemigroup.continuousMul, S.toAddSubmonoid.continuousAdd with }

/-- The (topological) closure of a non-unital subsemiring of a non-unital topological semiring is
itself a non-unital subsemiring. -/
def topologicalClosure (s : NonUnitalSubsemiring Œ±) : NonUnitalSubsemiring Œ± :=
  { s.toSubsemigroup.topologicalClosure, s.toAddSubmonoid.topologicalClosure with
    carrier := _root_.closure (s : Set Œ±) }

@[simp]
theorem topologicalClosure_coe (s : NonUnitalSubsemiring Œ±) :
    (s.topologicalClosure : Set Œ±) = _root_.closure (s : Set Œ±) :=
  rfl

theorem le_topologicalClosure (s : NonUnitalSubsemiring Œ±) : s ‚â§ s.topologicalClosure :=
  _root_.subset_closure

theorem isClosed_topologicalClosure (s : NonUnitalSubsemiring Œ±) :
    IsClosed (s.topologicalClosure : Set Œ±) := isClosed_closure

theorem topologicalClosure_minimal (s : NonUnitalSubsemiring Œ±) {t : NonUnitalSubsemiring Œ±}
    (h : s ‚â§ t) (ht : IsClosed (t : Set Œ±)) : s.topologicalClosure ‚â§ t :=
  closure_minimal h ht

/-- If a non-unital subsemiring of a non-unital topological semiring is commutative, then so is its
topological closure.

See note [reducible non-instances] -/
abbrev nonUnitalCommSemiringTopologicalClosure [T2Space Œ±] (s : NonUnitalSubsemiring Œ±)
    (hs : ‚àÄ x y : s, x * y = y * x) : NonUnitalCommSemiring s.topologicalClosure :=
  { NonUnitalSubsemiringClass.toNonUnitalSemiring s.topologicalClosure,
    s.toSubsemigroup.commSemigroupTopologicalClosure hs with }

end NonUnitalSubsemiring

variable [TopologicalSpace Œ±] [Semiring Œ±] [IsTopologicalSemiring Œ±]

instance : IsTopologicalSemiring (ULift Œ±) where

namespace Subsemiring

instance topologicalSemiring (S : Subsemiring Œ±) : IsTopologicalSemiring S :=
  { S.toSubmonoid.continuousMul, S.toAddSubmonoid.continuousAdd with }

instance continuousSMul (s : Subsemiring Œ±) (X) [TopologicalSpace X] [MulAction Œ± X]
    [ContinuousSMul Œ± X] : ContinuousSMul s X :=
  Submonoid.continuousSMul

end Subsemiring

/-- The (topological-space) closure of a subsemiring of a topological semiring is
itself a subsemiring. -/
def Subsemiring.topologicalClosure (s : Subsemiring Œ±) : Subsemiring Œ± :=
  { s.toSubmonoid.topologicalClosure, s.toAddSubmonoid.topologicalClosure with
    carrier := _root_.closure (s : Set Œ±) }

@[simp]
theorem Subsemiring.topologicalClosure_coe (s : Subsemiring Œ±) :
    (s.topologicalClosure : Set Œ±) = _root_.closure (s : Set Œ±) :=
  rfl

theorem Subsemiring.le_topologicalClosure (s : Subsemiring Œ±) : s ‚â§ s.topologicalClosure :=
  _root_.subset_closure

theorem Subsemiring.isClosed_topologicalClosure (s : Subsemiring Œ±) :
    IsClosed (s.topologicalClosure : Set Œ±) := isClosed_closure

theorem Subsemiring.topologicalClosure_minimal (s : Subsemiring Œ±) {t : Subsemiring Œ±} (h : s ‚â§ t)
    (ht : IsClosed (t : Set Œ±)) : s.topologicalClosure ‚â§ t :=
  closure_minimal h ht

/-- If a subsemiring of a topological semiring is commutative, then so is its
topological closure.

See note [reducible non-instances]. -/
abbrev Subsemiring.commSemiringTopologicalClosure [T2Space Œ±] (s : Subsemiring Œ±)
    (hs : ‚àÄ x y : s, x * y = y * x) : CommSemiring s.topologicalClosure :=
  { s.topologicalClosure.toSemiring, s.toSubmonoid.commMonoidTopologicalClosure hs with }

end

section

variable {Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤]

/-- The product topology on the cartesian product of two topological semirings
  makes the product into a topological semiring. -/
instance [NonUnitalNonAssocSemiring Œ±] [NonUnitalNonAssocSemiring Œ≤] [IsTopologicalSemiring Œ±]
    [IsTopologicalSemiring Œ≤] : IsTopologicalSemiring (Œ± √ó Œ≤) where

/-- The product topology on the cartesian product of two topological rings
  makes the product into a topological ring. -/
instance [NonUnitalNonAssocRing Œ±] [NonUnitalNonAssocRing Œ≤] [IsTopologicalRing Œ±]
    [IsTopologicalRing Œ≤] : IsTopologicalRing (Œ± √ó Œ≤) where

end

#adaptation_note /-- nightly-2024-04-08
needed to help `Pi.instIsTopologicalSemiring` -/
instance {Œ≤ : Type*} {C : Œ≤ ‚Üí Type*} [‚àÄ b, TopologicalSpace (C b)]
    [‚àÄ b, NonUnitalNonAssocSemiring (C b)] [‚àÄ b, IsTopologicalSemiring (C b)] :
    ContinuousAdd ((b : Œ≤) ‚Üí C b) :=
  inferInstance

instance Pi.instIsTopologicalSemiring {Œ≤ : Type*} {C : Œ≤ ‚Üí Type*} [‚àÄ b, TopologicalSpace (C b)]
    [‚àÄ b, NonUnitalNonAssocSemiring (C b)] [‚àÄ b, IsTopologicalSemiring (C b)] :
    IsTopologicalSemiring (‚àÄ b, C b) where

instance Pi.instIsTopologicalRing {Œ≤ : Type*} {C : Œ≤ ‚Üí Type*} [‚àÄ b, TopologicalSpace (C b)]
    [‚àÄ b, NonUnitalNonAssocRing (C b)] [‚àÄ b, IsTopologicalRing (C b)] :
    IsTopologicalRing (‚àÄ b, C b) := ‚ü®‚ü©

section MulOpposite

open MulOpposite

instance [NonUnitalNonAssocSemiring Œ±] [TopologicalSpace Œ±] [ContinuousAdd Œ±] :
    ContinuousAdd Œ±·µê·µí·µñ :=
  continuousAdd_induced opAddEquiv.symm

instance [NonUnitalNonAssocSemiring Œ±] [TopologicalSpace Œ±] [IsTopologicalSemiring Œ±] :
    IsTopologicalSemiring Œ±·µê·µí·µñ := ‚ü®‚ü©

instance [NonUnitalNonAssocRing Œ±] [TopologicalSpace Œ±] [ContinuousNeg Œ±] : ContinuousNeg Œ±·µê·µí·µñ :=
  opHomeomorph.symm.isInducing.continuousNeg fun _ => rfl

instance [NonUnitalNonAssocRing Œ±] [TopologicalSpace Œ±] [IsTopologicalRing Œ±] :
    IsTopologicalRing Œ±·µê·µí·µñ := ‚ü®‚ü©

end MulOpposite

section AddOpposite

open AddOpposite

instance [NonUnitalNonAssocSemiring Œ±] [TopologicalSpace Œ±] [ContinuousMul Œ±] :
    ContinuousMul Œ±·µÉ·µí·µñ :=
  continuousMul_induced opMulEquiv.symm

instance [NonUnitalNonAssocSemiring Œ±] [TopologicalSpace Œ±] [IsTopologicalSemiring Œ±] :
    IsTopologicalSemiring Œ±·µÉ·µí·µñ := ‚ü®‚ü©

instance [NonUnitalNonAssocRing Œ±] [TopologicalSpace Œ±] [IsTopologicalRing Œ±] :
    IsTopologicalRing Œ±·µÉ·µí·µñ := ‚ü®‚ü©

end AddOpposite

section

variable {R : Type*} [NonUnitalNonAssocRing R] [TopologicalSpace R]

theorem IsTopologicalRing.of_addGroup_of_nhds_zero [IsTopologicalAddGroup R]
    (hmul : Tendsto (uncurry ((¬∑ * ¬∑) : R ‚Üí R ‚Üí R)) (ùìù 0 √óÀ¢ ùìù 0) <| ùìù 0)
    (hmul_left : ‚àÄ x‚ÇÄ : R, Tendsto (fun x : R => x‚ÇÄ * x) (ùìù 0) <| ùìù 0)
    (hmul_right : ‚àÄ x‚ÇÄ : R, Tendsto (fun x : R => x * x‚ÇÄ) (ùìù 0) <| ùìù 0) : IsTopologicalRing R where
  continuous_mul := by
    refine continuous_of_continuousAt_zero‚ÇÇ (AddMonoidHom.mul (R := R)) ?_ ?_ ?_ <;>
      simpa only [ContinuousAt, mul_zero, zero_mul, nhds_prod_eq, AddMonoidHom.mul_apply]

theorem IsTopologicalRing.of_nhds_zero
    (hadd : Tendsto (uncurry ((¬∑ + ¬∑) : R ‚Üí R ‚Üí R)) (ùìù 0 √óÀ¢ ùìù 0) <| ùìù 0)
    (hneg : Tendsto (fun x => -x : R ‚Üí R) (ùìù 0) (ùìù 0))
    (hmul : Tendsto (uncurry ((¬∑ * ¬∑) : R ‚Üí R ‚Üí R)) (ùìù 0 √óÀ¢ ùìù 0) <| ùìù 0)
    (hmul_left : ‚àÄ x‚ÇÄ : R, Tendsto (fun x : R => x‚ÇÄ * x) (ùìù 0) <| ùìù 0)
    (hmul_right : ‚àÄ x‚ÇÄ : R, Tendsto (fun x : R => x * x‚ÇÄ) (ùìù 0) <| ùìù 0)
    (hleft : ‚àÄ x‚ÇÄ : R, ùìù x‚ÇÄ = map (fun x => x‚ÇÄ + x) (ùìù 0)) : IsTopologicalRing R :=
  have := IsTopologicalAddGroup.of_comm_of_nhds_zero hadd hneg hleft
  IsTopologicalRing.of_addGroup_of_nhds_zero hmul hmul_left hmul_right

end

variable [TopologicalSpace Œ±]

section

variable [NonUnitalNonAssocRing Œ±] [IsTopologicalRing Œ±]

instance : IsTopologicalRing (ULift Œ±) where

/-- In a topological semiring, the left-multiplication `AddMonoidHom` is continuous. -/
theorem mulLeft_continuous (x : Œ±) : Continuous (AddMonoidHom.mulLeft x) :=
  continuous_const.mul continuous_id

/-- In a topological semiring, the right-multiplication `AddMonoidHom` is continuous. -/
theorem mulRight_continuous (x : Œ±) : Continuous (AddMonoidHom.mulRight x) :=
  continuous_id.mul continuous_const

end

namespace NonUnitalSubring

variable [NonUnitalRing Œ±] [IsTopologicalRing Œ±]

instance instIsTopologicalRing (S : NonUnitalSubring Œ±) : IsTopologicalRing S :=
  { S.toSubsemigroup.continuousMul, inferInstanceAs (IsTopologicalAddGroup S.toAddSubgroup) with }

/-- The (topological) closure of a non-unital subring of a non-unital topological ring is
itself a non-unital subring. -/
def topologicalClosure (S : NonUnitalSubring Œ±) : NonUnitalSubring Œ± :=
  { S.toSubsemigroup.topologicalClosure, S.toAddSubgroup.topologicalClosure with
    carrier := _root_.closure (S : Set Œ±) }

theorem le_topologicalClosure (s : NonUnitalSubring Œ±) : s ‚â§ s.topologicalClosure :=
  _root_.subset_closure

theorem isClosed_topologicalClosure (s : NonUnitalSubring Œ±) :
    IsClosed (s.topologicalClosure : Set Œ±) := isClosed_closure

theorem topologicalClosure_minimal (s : NonUnitalSubring Œ±) {t : NonUnitalSubring Œ±} (h : s ‚â§ t)
    (ht : IsClosed (t : Set Œ±)) : s.topologicalClosure ‚â§ t :=
  closure_minimal h ht

/-- If a non-unital subring of a non-unital topological ring is commutative, then so is its
topological closure.

See note [reducible non-instances] -/
abbrev nonUnitalCommRingTopologicalClosure [T2Space Œ±] (s : NonUnitalSubring Œ±)
    (hs : ‚àÄ x y : s, x * y = y * x) : NonUnitalCommRing s.topologicalClosure :=
  { s.topologicalClosure.toNonUnitalRing, s.toSubsemigroup.commSemigroupTopologicalClosure hs with }

end NonUnitalSubring

variable [Ring Œ±] [IsTopologicalRing Œ±]

instance Subring.instIsTopologicalRing (S : Subring Œ±) : IsTopologicalRing S :=
  { S.toSubmonoid.continuousMul, inferInstanceAs (IsTopologicalAddGroup S.toAddSubgroup) with }

instance Subring.continuousSMul (s : Subring Œ±) (X) [TopologicalSpace X] [MulAction Œ± X]
    [ContinuousSMul Œ± X] : ContinuousSMul s X :=
  Subsemiring.continuousSMul s.toSubsemiring X

/-- The (topological-space) closure of a subring of a topological ring is
itself a subring. -/
def Subring.topologicalClosure (S : Subring Œ±) : Subring Œ± :=
  { S.toSubmonoid.topologicalClosure, S.toAddSubgroup.topologicalClosure with
    carrier := _root_.closure (S : Set Œ±) }

theorem Subring.le_topologicalClosure (s : Subring Œ±) : s ‚â§ s.topologicalClosure :=
  _root_.subset_closure

theorem Subring.isClosed_topologicalClosure (s : Subring Œ±) :
    IsClosed (s.topologicalClosure : Set Œ±) := isClosed_closure

theorem Subring.topologicalClosure_minimal (s : Subring Œ±) {t : Subring Œ±} (h : s ‚â§ t)
    (ht : IsClosed (t : Set Œ±)) : s.topologicalClosure ‚â§ t :=
  closure_minimal h ht

/-- If a subring of a topological ring is commutative, then so is its topological closure.

See note [reducible non-instances]. -/
abbrev Subring.commRingTopologicalClosure [T2Space Œ±] (s : Subring Œ±)
    (hs : ‚àÄ x y : s, x * y = y * x) : CommRing s.topologicalClosure :=
  { s.topologicalClosure.toRing, s.toSubmonoid.commMonoidTopologicalClosure hs with }

end IsTopologicalSemiring

/-!
### Lattice of ring topologies
We define a type class `RingTopology Œ±` which endows a ring `Œ±` with a topology such that all ring
operations are continuous.

Ring topologies on a fixed ring `Œ±` are ordered, by reverse inclusion. They form a complete lattice,
with `‚ä•` the discrete topology and `‚ä§` the indiscrete topology.

Any function `f : Œ± ‚Üí Œ≤` induces `coinduced f : TopologicalSpace Œ± ‚Üí RingTopology Œ≤`. -/


universe u v

/-- A ring topology on a ring `Œ±` is a topology for which addition, negation and multiplication
are continuous. -/
structure RingTopology (Œ± : Type u) [Ring Œ±] : Type u
  extends TopologicalSpace Œ±, IsTopologicalRing Œ±

namespace RingTopology

variable {Œ± : Type*} [Ring Œ±]

instance inhabited {Œ± : Type u} [Ring Œ±] : Inhabited (RingTopology Œ±) :=
  ‚ü®let _ : TopologicalSpace Œ± := ‚ä§;
    { continuous_add := continuous_top
      continuous_mul := continuous_top
      continuous_neg := continuous_top }‚ü©

theorem toTopologicalSpace_injective :
    Injective (toTopologicalSpace : RingTopology Œ± ‚Üí TopologicalSpace Œ±) := by
  intro f g _; cases f; cases g; congr

@[ext]
theorem ext {f g : RingTopology Œ±} (h : f.IsOpen = g.IsOpen) : f = g :=
  toTopologicalSpace_injective <| TopologicalSpace.ext h

/-- The ordering on ring topologies on the ring `Œ±`.
  `t ‚â§ s` if every set open in `s` is also open in `t` (`t` is finer than `s`). -/
instance : PartialOrder (RingTopology Œ±) :=
  PartialOrder.lift RingTopology.toTopologicalSpace toTopologicalSpace_injective

private def def_sInf (S : Set (RingTopology Œ±)) : RingTopology Œ± :=
  let _ := sInf (toTopologicalSpace '' S)
  { toContinuousAdd := continuousAdd_sInf <| forall_mem_image.2 fun t _ =>
      let _ := t.1; t.toContinuousAdd
    toContinuousMul := continuousMul_sInf <| forall_mem_image.2 fun t _ =>
      let _ := t.1; t.toContinuousMul
    toContinuousNeg := continuousNeg_sInf <| forall_mem_image.2 fun t _ =>
      let _ := t.1; t.toContinuousNeg }

/-- Ring topologies on `Œ±` form a complete lattice, with `‚ä•` the discrete topology and `‚ä§` the
indiscrete topology.

The infimum of a collection of ring topologies is the topology generated by all their open sets
(which is a ring topology).

The supremum of two ring topologies `s` and `t` is the infimum of the family of all ring topologies
contained in the intersection of `s` and `t`. -/
instance : CompleteSemilatticeInf (RingTopology Œ±) where
  sInf := def_sInf
  sInf_le := fun _ a haS => sInf_le (Œ± := TopologicalSpace Œ±) ‚ü®a, ‚ü®haS, rfl‚ü©‚ü©
  le_sInf := fun _ _ h => le_sInf (Œ± := TopologicalSpace Œ±) <| forall_mem_image.2 h

instance : CompleteLattice (RingTopology Œ±) :=
  completeLatticeOfCompleteSemilatticeInf _

/-- Given `f : Œ± ‚Üí Œ≤` and a topology on `Œ±`, the coinduced ring topology on `Œ≤` is the finest
topology such that `f` is continuous and `Œ≤` is a topological ring. -/
def coinduced {Œ± Œ≤ : Type*} [t : TopologicalSpace Œ±] [Ring Œ≤] (f : Œ± ‚Üí Œ≤) : RingTopology Œ≤ :=
  sInf { b : RingTopology Œ≤ | t.coinduced f ‚â§ b.toTopologicalSpace }

theorem coinduced_continuous {Œ± Œ≤ : Type*} [t : TopologicalSpace Œ±] [Ring Œ≤] (f : Œ± ‚Üí Œ≤) :
    Continuous[t, (coinduced f).toTopologicalSpace] f :=
  continuous_sInf_rng.2 <| forall_mem_image.2 fun _ => continuous_iff_coinduced_le.2

/-- The forgetful functor from ring topologies on `a` to additive group topologies on `a`. -/
def toAddGroupTopology (t : RingTopology Œ±) : AddGroupTopology Œ± where
  toTopologicalSpace := t.toTopologicalSpace
  toIsTopologicalAddGroup :=
    @IsTopologicalRing.to_topologicalAddGroup _ _ t.toTopologicalSpace t.toIsTopologicalRing

/-- The order embedding from ring topologies on `a` to additive group topologies on `a`. -/
def toAddGroupTopology.orderEmbedding : OrderEmbedding (RingTopology Œ±) (AddGroupTopology Œ±) :=
  OrderEmbedding.ofMapLEIff toAddGroupTopology fun _ _ => Iff.rfl

end RingTopology

section AbsoluteValue

/-- Construct an absolute value on a semiring `T` from an absolute value on a semiring `R`
and an injective ring homomorphism `f : T ‚Üí+* R` -/
def AbsoluteValue.comp {R S T : Type*} [Semiring T] [Semiring R] [Semiring S] [PartialOrder S]
    (v : AbsoluteValue R S) {f : T ‚Üí+* R} (hf : Function.Injective f) : AbsoluteValue T S where
  toMulHom := v.1.comp f
  nonneg' _ := v.nonneg _
  eq_zero' _ := v.eq_zero.trans (map_eq_zero_iff f hf)
  add_le' _ _ := (congr_arg v (map_add f _ _)).trans_le (v.add_le _ _)

end AbsoluteValue
