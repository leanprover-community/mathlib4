/-
Copyright (c) 2022 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import Mathlib.Topology.Algebra.Constructions
import Mathlib.Topology.ContinuousMap.Defs
import Mathlib.Algebra.Star.Basic

/-!
# Continuity of `star`

This file defines the `ContinuousStar` typeclass, along with instances on `Pi`, `Prod`,
`MulOpposite`, and `Units`.
-/

open Filter Topology

/-- Basic hypothesis to talk about a topological space with a continuous `star` operator. -/
class ContinuousStar (R : Type*) [TopologicalSpace R] [Star R] : Prop where
  /-- The `star` operator is continuous. -/
  continuous_star : Continuous (star : R ‚Üí R)

export ContinuousStar (continuous_star)

section Continuity

variable {Œ± R : Type*} [TopologicalSpace R] [Star R] [ContinuousStar R]

theorem continuousOn_star {s : Set R} : ContinuousOn star s :=
  continuous_star.continuousOn

theorem continuousWithinAt_star {s : Set R} {x : R} : ContinuousWithinAt star s x :=
  continuous_star.continuousWithinAt

theorem continuousAt_star {x : R} : ContinuousAt star x :=
  continuous_star.continuousAt

theorem tendsto_star (a : R) : Tendsto star (ùìù a) (ùìù (star a)) :=
  continuousAt_star

theorem Filter.Tendsto.star {f : Œ± ‚Üí R} {l : Filter Œ±} {y : R} (h : Tendsto f l (ùìù y)) :
    Tendsto (fun x => star (f x)) l (ùìù (star y)) :=
  (continuous_star.tendsto y).comp h

variable [TopologicalSpace Œ±] {f : Œ± ‚Üí R} {s : Set Œ±} {x : Œ±}

@[continuity, fun_prop]
theorem Continuous.star (hf : Continuous f) : Continuous fun x => star (f x) :=
  continuous_star.comp hf

@[fun_prop]
theorem ContinuousAt.star (hf : ContinuousAt f x) : ContinuousAt (fun x => star (f x)) x :=
  continuousAt_star.comp hf

@[fun_prop]
theorem ContinuousOn.star (hf : ContinuousOn f s) : ContinuousOn (fun x => star (f x)) s :=
  continuous_star.comp_continuousOn hf

theorem ContinuousWithinAt.star (hf : ContinuousWithinAt f s x) :
    ContinuousWithinAt (fun x => star (f x)) s x :=
  Filter.Tendsto.star hf

/-- The star operation bundled as a continuous map. -/
@[simps]
def starContinuousMap : C(R, R) :=
  ‚ü®star, continuous_star‚ü©

end Continuity

section Instances

variable {R S Œπ : Type*}

instance [Star R] [Star S] [TopologicalSpace R] [TopologicalSpace S] [ContinuousStar R]
    [ContinuousStar S] : ContinuousStar (R √ó S) :=
  ‚ü®(continuous_star.comp continuous_fst).prodMk (continuous_star.comp continuous_snd)‚ü©

instance {C : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (C i)] [‚àÄ i, Star (C i)]
    [‚àÄ i, ContinuousStar (C i)] : ContinuousStar (‚àÄ i, C i) where
  continuous_star := continuous_pi fun i => Continuous.star (continuous_apply i)

instance [Star R] [TopologicalSpace R] [ContinuousStar R] : ContinuousStar R·µê·µí·µñ :=
  ‚ü®MulOpposite.continuous_op.comp <| MulOpposite.continuous_unop.star‚ü©

instance [Monoid R] [StarMul R] [TopologicalSpace R] [ContinuousStar R] :
    ContinuousStar RÀ£ :=
  ‚ü®continuous_induced_rng.2 Units.continuous_embedProduct.star‚ü©

end Instances
