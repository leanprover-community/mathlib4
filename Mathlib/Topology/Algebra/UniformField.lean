/-
Copyright (c) 2019 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
import Mathlib.Algebra.Field.Subfield
import Mathlib.Topology.Algebra.Field
import Mathlib.Topology.Algebra.UniformRing

#align_import topology.algebra.uniform_field from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Completion of topological fields

The goal of this file is to prove the main part of Proposition 7 of Bourbaki GT III 6.8 :

The completion `hat K` of a Hausdorff topological field is a field if the image under
the mapping `x â†¦ xâ»Â¹` of every Cauchy filter (with respect to the additive uniform structure)
which does not have a cluster point at `0` is a Cauchy filter
(with respect to the additive uniform structure).

Bourbaki does not give any detail here, he refers to the general discussion of extending
functions defined on a dense subset with values in a complete Hausdorff space. In particular
the subtlety about clustering at zero is totally left to readers.

Note that the separated completion of a non-separated topological field is the zero ring, hence
the separation assumption is needed. Indeed the kernel of the completion map is the closure of
zero which is an ideal. Hence it's either zero (and the field is separated) or the full field,
which implies one is sent to zero and the completion ring is trivial.

The main definition is `CompletableTopField` which packages the assumptions as a Prop-valued
type class and the main results are the instances `UniformSpace.Completion.Field` and
`UniformSpace.Completion.TopologicalDivisionRing`.
-/


noncomputable section

open scoped Classical
open uniformity Topology

open Set UniformSpace UniformSpace.Completion Filter

variable (K : Type*) [Field K] [UniformSpace K]

local notation "hat" => Completion

/-- A topological field is completable if it is separated and the image under
the mapping x â†¦ xâ»Â¹ of every Cauchy filter (with respect to the additive uniform structure)
which does not have a cluster point at 0 is a Cauchy filter
(with respect to the additive uniform structure). This ensures the completion is
a field.
-/
class CompletableTopField extends T0Space K : Prop where
  nice : âˆ€ F : Filter K, Cauchy F â†’ ğ“ 0 âŠ“ F = âŠ¥ â†’ Cauchy (map (fun x => xâ»Â¹) F)
#align completable_top_field CompletableTopField

namespace UniformSpace

namespace Completion

instance (priority := 100) [T0Space K] : Nontrivial (hat K) :=
  âŸ¨âŸ¨0, 1, fun h => zero_ne_one <| (uniformEmbedding_coe K).inj hâŸ©âŸ©

variable {K}

/-- extension of inversion to the completion of a field. -/
def hatInv : hat K â†’ hat K :=
  denseInducing_coe.extend fun x : K => (â†‘xâ»Â¹ : hat K)
#align uniform_space.completion.hat_inv UniformSpace.Completion.hatInv

theorem continuous_hatInv [CompletableTopField K] {x : hat K} (h : x â‰  0) :
    ContinuousAt hatInv x := by
  refine denseInducing_coe.continuousAt_extend ?_
  apply mem_of_superset (compl_singleton_mem_nhds h)
  intro y y_ne
  rw [mem_compl_singleton_iff] at y_ne
  apply CompleteSpace.complete
  have : (fun (x : K) => (â†‘xâ»Â¹: hat K)) =
      ((fun (y : K) => (â†‘y: hat K))âˆ˜(fun (x : K) => (xâ»Â¹ : K))) := by
    unfold Function.comp
    simp
  rw [this, â† Filter.map_map]
  apply Cauchy.map _ (Completion.uniformContinuous_coe K)
  apply CompletableTopField.nice
  Â· haveI := denseInducing_coe.comap_nhds_neBot y
    apply cauchy_nhds.comap
    rw [Completion.comap_coe_eq_uniformity]
  Â· have eq_bot : ğ“ (0 : hat K) âŠ“ ğ“ y = âŠ¥ := by
      by_contra h
      exact y_ne (eq_of_nhds_neBot <| neBot_iff.mpr h).symm
    erw [denseInducing_coe.nhds_eq_comap (0 : K), â† Filter.comap_inf, eq_bot]
    exact comap_bot
#align uniform_space.completion.continuous_hat_inv UniformSpace.Completion.continuous_hatInv

/-
The value of `hat_inv` at zero is not really specified, although it's probably zero.
Here we explicitly enforce the `inv_zero` axiom.
-/
instance instInvCompletion : Inv (hat K) :=
  âŸ¨fun x => if x = 0 then 0 else hatInv xâŸ©

variable [TopologicalDivisionRing K]

theorem hatInv_extends {x : K} (h : x â‰  0) : hatInv (x : hat K) = â†‘(xâ»Â¹ : K) :=
  denseInducing_coe.extend_eq_at ((continuous_coe K).continuousAt.comp (continuousAt_invâ‚€ h))
#align uniform_space.completion.hat_inv_extends UniformSpace.Completion.hatInv_extends

variable [CompletableTopField K]

@[norm_cast]
theorem coe_inv (x : K) : (x : hat K)â»Â¹ = ((xâ»Â¹ : K) : hat K) := by
  by_cases h : x = 0
  Â· rw [h, inv_zero]
    dsimp [Inv.inv]
    norm_cast
    simp
  Â· conv_lhs => dsimp [Inv.inv]
    rw [if_neg]
    Â· exact hatInv_extends h
    Â· exact fun H => h (denseEmbedding_coe.inj H)
#align uniform_space.completion.coe_inv UniformSpace.Completion.coe_inv

variable [UniformAddGroup K]

theorem mul_hatInv_cancel {x : hat K} (x_ne : x â‰  0) : x * hatInv x = 1 := by
  haveI : T1Space (hat K) := T2Space.t1Space
  let f := fun x : hat K => x * hatInv x
  let c := (fun (x : K) => (x : hat K))
  change f x = 1
  have cont : ContinuousAt f x := by
    letI : TopologicalSpace (hat K Ã— hat K) := instTopologicalSpaceProd
    have : ContinuousAt (fun y : hat K => ((y, hatInv y) : hat K Ã— hat K)) x :=
      continuous_id.continuousAt.prod (continuous_hatInv x_ne)
    exact (_root_.continuous_mul.continuousAt.comp this : _)
  have clo : x âˆˆ closure (c '' {0}á¶œ) := by
    have := denseInducing_coe.dense x
    rw [â† image_univ, show (univ : Set K) = {0} âˆª {0}á¶œ from (union_compl_self _).symm,
      image_union] at this
    apply mem_closure_of_mem_closure_union this
    rw [image_singleton]
    exact compl_singleton_mem_nhds x_ne
  have fxclo : f x âˆˆ closure (f '' (c '' {0}á¶œ)) := mem_closure_image cont clo
  have : f '' (c '' {0}á¶œ) âŠ† {1} := by
    rw [image_image]
    rintro _ âŸ¨z, z_ne, rflâŸ©
    rw [mem_singleton_iff]
    rw [mem_compl_singleton_iff] at z_ne
    dsimp [f]
    rw [hatInv_extends z_ne, â† coe_mul]
    rw [mul_inv_cancel z_ne, coe_one]
  replace fxclo := closure_mono this fxclo
  rwa [closure_singleton, mem_singleton_iff] at fxclo
#align uniform_space.completion.mul_hat_inv_cancel UniformSpace.Completion.mul_hatInv_cancel

instance instField : Field (hat K) where
  exists_pair_ne := âŸ¨0, 1, fun h => zero_ne_one ((uniformEmbedding_coe K).inj h)âŸ©
  mul_inv_cancel := fun x x_ne => by simp only [Inv.inv, if_neg x_ne, mul_hatInv_cancel x_ne]
  inv_zero := by simp only [Inv.inv, ite_true]
  -- TODO: use a better defeq
  nnqsmul := _
  qsmul := _
#align uniform_space.completion.field UniformSpace.Completion.instField

instance : TopologicalDivisionRing (hat K) :=
  { Completion.topologicalRing with
    continuousAt_invâ‚€ := by
      intro x x_ne
      have : { y | hatInv y = yâ»Â¹ } âˆˆ ğ“ x :=
        haveI : {(0 : hat K)}á¶œ âŠ† { y : hat K | hatInv y = yâ»Â¹ } := by
          intro y y_ne
          rw [mem_compl_singleton_iff] at y_ne
          dsimp [Inv.inv]
          rw [if_neg y_ne]
        mem_of_superset (compl_singleton_mem_nhds x_ne) this
      exact ContinuousAt.congr (continuous_hatInv x_ne) this }

end Completion

end UniformSpace

variable (L : Type*) [Field L] [UniformSpace L] [CompletableTopField L]

instance Subfield.completableTopField (K : Subfield L) : CompletableTopField K where
  nice F F_cau inf_F := by
    let i : K â†’+* L := K.subtype
    have hi : UniformInducing i := uniformEmbedding_subtype_val.toUniformInducing
    rw [â† hi.cauchy_map_iff] at F_cau âŠ¢
    rw [map_comm (show (i âˆ˜ fun x => xâ»Â¹) = (fun x => xâ»Â¹) âˆ˜ i by ext; rfl)]
    apply CompletableTopField.nice _ F_cau
    rw [â† Filter.push_pull', â† map_zero i, â† hi.inducing.nhds_eq_comap, inf_F, Filter.map_bot]
#align subfield.completable_top_field Subfield.completableTopField

instance (priority := 100) completableTopField_of_complete (L : Type*) [Field L] [UniformSpace L]
    [TopologicalDivisionRing L] [T0Space L] [CompleteSpace L] : CompletableTopField L where
  nice F cau_F hF := by
    haveI : NeBot F := cau_F.1
    rcases CompleteSpace.complete cau_F with âŸ¨x, hxâŸ©
    have hx' : x â‰  0 := by
      rintro rfl
      rw [inf_eq_right.mpr hx] at hF
      exact cau_F.1.ne hF
    exact Filter.Tendsto.cauchy_map <|
      calc
        map (fun x => xâ»Â¹) F â‰¤ map (fun x => xâ»Â¹) (ğ“ x) := map_mono hx
        _ â‰¤ ğ“ xâ»Â¹ := continuousAt_invâ‚€ hx'
#align completable_top_field_of_complete completableTopField_of_complete
