/-
Copyright (c) 2020 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis, Attila GÃ¡spÃ¡r
-/
import Mathlib.LinearAlgebra.AffineSpace.AffineMap
import Mathlib.LinearAlgebra.AffineSpace.Midpoint
import Mathlib.Topology.Algebra.Group.AddTorsor

/-!
# Topological properties of affine spaces and maps

This file contains a few facts regarding the continuity of affine maps.
-/


namespace AffineMap

variable
  {R V P W Q : Type*}
  [AddCommGroup V] [TopologicalSpace V]
  [AddTorsor V P] [TopologicalSpace P] [IsTopologicalAddTorsor P]
  [AddCommGroup W] [TopologicalSpace W]
  [AddTorsor W Q] [TopologicalSpace Q] [IsTopologicalAddTorsor Q]

section Ring

variable [Ring R] [Module R V] [Module R W]

/-- If `f` is an affine map, then its linear part is continuous iff `f` is continuous. -/
theorem continuous_linear_iff {f : P â†’áµƒ[R] Q} : Continuous f.linear â†” Continuous f := by
  inhabit P
  have :
    (f.linear : V â†’ W) =
      (Homeomorph.vaddConst <| f default).symm âˆ˜ f âˆ˜ (Homeomorph.vaddConst default) := by
    ext v
    simp
  rw [this]
  simp only [Homeomorph.comp_continuous_iff, Homeomorph.comp_continuous_iff']

/-- An affine map is continuous iff its underlying linear map is continuous. See also
`AffineMap.continuous_linear_iff`. -/
@[deprecated continuous_linear_iff (since := "2025-09-13")]
theorem continuous_iff {f : P â†’áµƒ[R] Q} : Continuous f â†” Continuous f.linear :=
  continuous_linear_iff.symm

/-- If `f` is an affine map, then its linear part is an open map iff `f` is an open map. -/
theorem isOpenMap_linear_iff {f : P â†’áµƒ[R] Q} : IsOpenMap f.linear â†” IsOpenMap f := by
  inhabit P
  have :
    (f.linear : V â†’ W) =
      (Homeomorph.vaddConst <| f default).symm âˆ˜ f âˆ˜ (Homeomorph.vaddConst default) := by
    ext v
    simp
  rw [this]
  simp only [Homeomorph.comp_isOpenMap_iff, Homeomorph.comp_isOpenMap_iff']

variable [TopologicalSpace R] [ContinuousSMul R V]

/-- The line map is continuous in all arguments. -/
@[continuity, fun_prop]
theorem lineMap_continuous_uncurry :
    Continuous (fun pqt : P Ã— P Ã— R â†¦ lineMap pqt.1 pqt.2.1 pqt.2.2) := by
  simp only [coe_lineMap]
  fun_prop

/-- The line map is continuous. -/
theorem lineMap_continuous {p q : P} :
    Continuous (lineMap p q : R â†’áµƒ[R] P) := by
  fun_prop

open Topology Filter

section Tendsto

variable {Î± : Type*} {l : Filter Î±}

theorem _root_.Filter.Tendsto.lineMap {fâ‚ fâ‚‚ : Î± â†’ P} {g : Î± â†’ R} {pâ‚ pâ‚‚ : P} {c : R}
    (hâ‚ : Tendsto fâ‚ l (ğ“ pâ‚)) (hâ‚‚ : Tendsto fâ‚‚ l (ğ“ pâ‚‚)) (hg : Tendsto g l (ğ“ c)) :
    Tendsto (fun x => AffineMap.lineMap (fâ‚ x) (fâ‚‚ x) (g x)) l (ğ“ <| AffineMap.lineMap pâ‚ pâ‚‚ c) :=
  (hg.smul (hâ‚‚.vsub hâ‚)).vadd hâ‚

theorem _root_.Filter.Tendsto.midpoint [Invertible (2 : R)] {fâ‚ fâ‚‚ : Î± â†’ P} {pâ‚ pâ‚‚ : P}
    (hâ‚ : Tendsto fâ‚ l (ğ“ pâ‚)) (hâ‚‚ : Tendsto fâ‚‚ l (ğ“ pâ‚‚)) :
    Tendsto (fun x => midpoint R (fâ‚ x) (fâ‚‚ x)) l (ğ“ <| midpoint R pâ‚ pâ‚‚) :=
  hâ‚.lineMap hâ‚‚ tendsto_const_nhds

end Tendsto

variable {X : Type*} [TopologicalSpace X] {fâ‚ fâ‚‚ : X â†’ P} {g : X â†’ R} {s : Set X} {x : X}

@[fun_prop]
theorem _root_.ContinuousWithinAt.lineMap (hâ‚ : ContinuousWithinAt fâ‚ s x)
    (hâ‚‚ : ContinuousWithinAt fâ‚‚ s x) (hg : ContinuousWithinAt g s x) :
    ContinuousWithinAt (fun x â†¦ lineMap (fâ‚ x) (fâ‚‚ x) (g x)) s x :=
  Tendsto.lineMap hâ‚ hâ‚‚ hg

theorem _root_.ContinuousAt.lineMap (hâ‚ : ContinuousAt fâ‚ x) (hâ‚‚ : ContinuousAt fâ‚‚ x)
    (hg : ContinuousAt g x) :
    ContinuousAt (fun x â†¦ lineMap (fâ‚ x) (fâ‚‚ x) (g x)) x := by
  fun_prop

theorem _root_.ContinuousOn.lineMap (hâ‚ : ContinuousOn fâ‚ s) (hâ‚‚ : ContinuousOn fâ‚‚ s)
    (hg : ContinuousOn g s) :
    ContinuousOn (fun x â†¦ lineMap (fâ‚ x) (fâ‚‚ x) (g x)) s := by
  fun_prop

theorem _root_.Continuous.lineMap (hâ‚ : Continuous fâ‚) (hâ‚‚ : Continuous fâ‚‚)
    (hg : Continuous g) :
    Continuous (fun x â†¦ lineMap (fâ‚ x) (fâ‚‚ x) (g x)) := by
  fun_prop

end Ring

section CommRing

variable [CommRing R] [Module R V] [ContinuousConstSMul R V]

@[continuity, fun_prop]
theorem homothety_continuous (x : P) (t : R) : Continuous <| homothety x t := by
  rw [coe_homothety]
  fun_prop

variable (R) [TopologicalSpace R] [Module R W] [ContinuousSMul R W] (x : Q) {s : Set Q}

open Topology

theorem _root_.eventually_homothety_mem_of_mem_interior {y : Q} (hy : y âˆˆ interior s) :
    âˆ€á¶  Î´ in ğ“ (1 : R), homothety x Î´ y âˆˆ s := by
  have cont : Continuous (fun Î´ : R => homothety x Î´ y) := lineMap_continuous
  filter_upwards [cont.tendsto' 1 y (by simp) |>.eventually (isOpen_interior.eventually_mem hy)]
    with _ h using interior_subset h

theorem _root_.eventually_homothety_image_subset_of_finite_subset_interior {t : Set Q}
    (ht : t.Finite) (h : t âŠ† interior s) : âˆ€á¶  Î´ in ğ“ (1 : R), homothety x Î´ '' t âŠ† s := by
  suffices âˆ€ y âˆˆ t, âˆ€á¶  Î´ in ğ“ (1 : R), homothety x Î´ y âˆˆ s by
    simp_rw [Set.image_subset_iff]
    exact (Filter.eventually_all_finite ht).mpr this
  intro y hy
  exact eventually_homothety_mem_of_mem_interior R x (h hy)

end CommRing

section Field

variable [Field R] [Module R V] [ContinuousConstSMul R V]

theorem homothety_isOpenMap (x : P) (t : R) (ht : t â‰  0) : IsOpenMap <| homothety x t := by
  apply IsOpenMap.of_inverse (homothety_continuous x tâ»Â¹) <;> intro e <;>
    simp [â† AffineMap.comp_apply, â† homothety_mul, ht]

end Field

end AffineMap
