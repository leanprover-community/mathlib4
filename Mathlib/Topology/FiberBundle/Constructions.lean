/-
Copyright (c) 2022 Nicol√≤ Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Nicol√≤ Cavalleri, S√©bastien Gou√´zel, Heather Macbeth, Floris van Doorn
-/
import Mathlib.Topology.FiberBundle.Basic

/-!
# Standard constructions on fiber bundles

This file contains several standard constructions on fiber bundles:

* `Bundle.Trivial.fiberBundle ùïú B F`: the trivial fiber bundle with model fiber `F` over the base
  `B`

* `FiberBundle.prod`: for fiber bundles `E‚ÇÅ` and `E‚ÇÇ` over a common base, a fiber bundle structure
  on their fiberwise product `E‚ÇÅ √ó·µá E‚ÇÇ` (the notation stands for `fun x ‚Ü¶ E‚ÇÅ x √ó E‚ÇÇ x`).

* `FiberBundle.pullback`: for a fiber bundle `E` over `B`, a fiber bundle structure on its
  pullback `f *·µñ E` by a map `f : B' ‚Üí B` (the notation is a type synonym for `E ‚àò f`).

## Tags

fiber bundle, fibre bundle, fiberwise product, pullback

-/

open Bundle Filter Set TopologicalSpace Topology

/-! ### The trivial bundle -/

namespace Bundle

namespace Trivial

variable (B : Type*) (F : Type*)

-- TODO: use `TotalSpace.toProd`
instance topologicalSpace [t‚ÇÅ : TopologicalSpace B]
    [t‚ÇÇ : TopologicalSpace F] : TopologicalSpace (TotalSpace F (Trivial B F)) :=
  induced TotalSpace.proj t‚ÇÅ ‚äì induced (TotalSpace.trivialSnd B F) t‚ÇÇ

variable [TopologicalSpace B] [TopologicalSpace F]

theorem isInducing_toProd : IsInducing (TotalSpace.toProd B F) :=
  ‚ü®by simp only [instTopologicalSpaceProd, induced_inf, induced_compose]; rfl‚ü©

@[deprecated (since := "2024-10-28")] alias inducing_toProd := isInducing_toProd

/-- Homeomorphism between the total space of the trivial bundle and the Cartesian product. -/
def homeomorphProd : TotalSpace F (Trivial B F) ‚âÉ‚Çú B √ó F :=
  (TotalSpace.toProd _ _).toHomeomorphOfIsInducing (isInducing_toProd B F)

/-- Local trivialization for trivial bundle. -/
def trivialization : Trivialization F (œÄ F (Bundle.Trivial B F)) where
  toPartialHomeomorph := (homeomorphProd B F).toPartialHomeomorph
  baseSet := univ
  open_baseSet := isOpen_univ
  source_eq := rfl
  target_eq := univ_prod_univ.symm
  proj_toFun _ _ := rfl

@[simp]
theorem trivialization_source : (trivialization B F).source = univ := rfl

@[simp]
theorem trivialization_target : (trivialization B F).target = univ := rfl

/-- Fiber bundle instance on the trivial bundle. -/
instance fiberBundle : FiberBundle F (Bundle.Trivial B F) where
  trivializationAtlas' := {trivialization B F}
  trivializationAt' _ := trivialization B F
  mem_baseSet_trivializationAt' := mem_univ
  trivialization_mem_atlas' _ := mem_singleton _
  totalSpaceMk_isInducing' _ := (homeomorphProd B F).symm.isInducing.comp
    (isInducing_const_prod.2 .id)

theorem eq_trivialization (e : Trivialization F (œÄ F (Bundle.Trivial B F)))
    [i : MemTrivializationAtlas e] : e = trivialization B F := i.out

end Trivial

end Bundle

/-! ### Fibrewise product of two bundles -/


section Prod

variable {B : Type*}

section Defs

variable (F‚ÇÅ : Type*) (E‚ÇÅ : B ‚Üí Type*) (F‚ÇÇ : Type*) (E‚ÇÇ : B ‚Üí Type*)
variable [TopologicalSpace (TotalSpace F‚ÇÅ E‚ÇÅ)] [TopologicalSpace (TotalSpace F‚ÇÇ E‚ÇÇ)]

/-- Equip the total space of the fiberwise product of two fiber bundles `E‚ÇÅ`, `E‚ÇÇ` with
the induced topology from the diagonal embedding into `TotalSpace F‚ÇÅ E‚ÇÅ √ó TotalSpace F‚ÇÇ E‚ÇÇ`. -/
instance FiberBundle.Prod.topologicalSpace : TopologicalSpace (TotalSpace (F‚ÇÅ √ó F‚ÇÇ) (E‚ÇÅ √ó·µá E‚ÇÇ)) :=
  TopologicalSpace.induced
    (fun p ‚Ü¶ ((‚ü®p.1, p.2.1‚ü© : TotalSpace F‚ÇÅ E‚ÇÅ), (‚ü®p.1, p.2.2‚ü© : TotalSpace F‚ÇÇ E‚ÇÇ)))
    inferInstance

/-- The diagonal map from the total space of the fiberwise product of two fiber bundles
`E‚ÇÅ`, `E‚ÇÇ` into `TotalSpace F‚ÇÅ E‚ÇÅ √ó TotalSpace F‚ÇÇ E‚ÇÇ` is an inducing map. -/
theorem FiberBundle.Prod.isInducing_diag :
    IsInducing (fun p ‚Ü¶ (‚ü®p.1, p.2.1‚ü©, ‚ü®p.1, p.2.2‚ü©) :
      TotalSpace (F‚ÇÅ √ó F‚ÇÇ) (E‚ÇÅ √ó·µá E‚ÇÇ) ‚Üí TotalSpace F‚ÇÅ E‚ÇÅ √ó TotalSpace F‚ÇÇ E‚ÇÇ) :=
  ‚ü®rfl‚ü©

@[deprecated (since := "2024-10-28")]
alias FiberBundle.Prod.inducing_diag := FiberBundle.Prod.isInducing_diag

/-- For vector bundles `E‚ÇÅ` and `E‚ÇÇ` over a manifold `B`, the natural projection from the
total space of `E‚ÇÅ √ó·µá E‚ÇÇ` to the total space of `E‚ÇÅ` is continuous. -/
theorem FiberBundle.Prod.continuous_fst : Continuous (TotalSpace.Prod.fst F‚ÇÅ F‚ÇÇ E‚ÇÅ E‚ÇÇ) :=
  _root_.continuous_fst.comp (FiberBundle.Prod.isInducing_diag F‚ÇÅ E‚ÇÅ F‚ÇÇ E‚ÇÇ).continuous

/-- For fiber bundles `E‚ÇÅ` and `E‚ÇÇ` over a manifold `B`, the natural projection from the
total space of `E‚ÇÅ √ó·µá E‚ÇÇ` to the total space of `E‚ÇÅ` is continuous. -/
theorem FiberBundle.Prod.continuous_snd : Continuous (TotalSpace.Prod.snd F‚ÇÅ F‚ÇÇ E‚ÇÅ E‚ÇÇ) :=
  _root_.continuous_snd.comp (FiberBundle.Prod.isInducing_diag F‚ÇÅ E‚ÇÅ F‚ÇÇ E‚ÇÇ).continuous

variable {F‚ÇÅ F‚ÇÇ E‚ÇÅ E‚ÇÇ} in
/-- Given fiber bundles `E‚ÇÅ`, `E‚ÇÇ` over a space `B`, if `œÜ` is a map into the total space of
`E‚ÇÅ √ó·µá E‚ÇÇ`, then its continuity can be checked by checking the continuity of (1) the map
`TotalSpace.Prod.fst ‚àò œÜ` into the total space of `E‚ÇÅ`, and (2) the map `TotalSpace.Prod.snd ‚àò œÜ`
into the total space of `E‚ÇÇ`. -/
theorem FiberBundle.Prod.continuous_of_continuous_fst_comp_of_continuous_snd_comp
    {M : Type*} [TopologicalSpace M] {œÜ : M ‚Üí TotalSpace (F‚ÇÅ √ó F‚ÇÇ) (E‚ÇÅ √ó·µá E‚ÇÇ)}
    (h1 : Continuous (TotalSpace.Prod.fst F‚ÇÅ F‚ÇÇ E‚ÇÅ E‚ÇÇ ‚àò œÜ))
    (h2 : Continuous (TotalSpace.Prod.snd F‚ÇÅ F‚ÇÇ E‚ÇÅ E‚ÇÇ ‚àò œÜ)) :
    Continuous œÜ := by
  rw [(FiberBundle.Prod.isInducing_diag F‚ÇÅ E‚ÇÅ F‚ÇÇ E‚ÇÇ).continuous_iff]
  exact h1.prod_mk h2

variable {F‚ÇÅ F‚ÇÇ E‚ÇÅ E‚ÇÇ} in
/-- Given fiber bundles `E‚ÇÅ`, `E‚ÇÇ` over a space `B`, a map `œÜ` into the total space of `E‚ÇÅ √ó·µá E‚ÇÇ` is
continuous if and only if the following two maps are continuous: (1) the map
`TotalSpace.Prod.fst ‚àò œÜ` into the total space of `E‚ÇÅ`, and (2) the map `TotalSpace.Prod.snd ‚àò œÜ`
into the total space of `E‚ÇÇ`. -/
theorem FiberBundle.Prod.continuous_iff_continuous_fst_comp_and_continuous_snd_comp
    {M : Type*} [TopologicalSpace M] (œÜ : M ‚Üí TotalSpace (F‚ÇÅ √ó F‚ÇÇ) (E‚ÇÅ √ó·µá E‚ÇÇ)) :
    Continuous œÜ ‚Üî (Continuous (TotalSpace.Prod.fst F‚ÇÅ F‚ÇÇ E‚ÇÅ E‚ÇÇ ‚àò œÜ)
    ‚àß Continuous (TotalSpace.Prod.snd F‚ÇÅ F‚ÇÇ E‚ÇÅ E‚ÇÇ ‚àò œÜ)) := by
  refine ‚ü®fun h ‚Ü¶ ‚ü®?_, ?_‚ü©, fun ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚Ü¶ ?_‚ü©
  ¬∑ exact (FiberBundle.Prod.continuous_fst F‚ÇÅ E‚ÇÅ F‚ÇÇ E‚ÇÇ).comp h
  ¬∑ exact (FiberBundle.Prod.continuous_snd F‚ÇÅ E‚ÇÅ F‚ÇÇ E‚ÇÇ).comp h
  ¬∑ exact FiberBundle.Prod.continuous_of_continuous_fst_comp_of_continuous_snd_comp h‚ÇÅ h‚ÇÇ

end Defs

open FiberBundle

variable [TopologicalSpace B] (F‚ÇÅ : Type*) [TopologicalSpace F‚ÇÅ] (E‚ÇÅ : B ‚Üí Type*)
  [TopologicalSpace (TotalSpace F‚ÇÅ E‚ÇÅ)] (F‚ÇÇ : Type*) [TopologicalSpace F‚ÇÇ] (E‚ÇÇ : B ‚Üí Type*)
  [TopologicalSpace (TotalSpace F‚ÇÇ E‚ÇÇ)]

namespace Trivialization

variable {F‚ÇÅ E‚ÇÅ F‚ÇÇ E‚ÇÇ}
variable (e‚ÇÅ : Trivialization F‚ÇÅ (œÄ F‚ÇÅ E‚ÇÅ)) (e‚ÇÇ : Trivialization F‚ÇÇ (œÄ F‚ÇÇ E‚ÇÇ))

/-- Given trivializations `e‚ÇÅ`, `e‚ÇÇ` for fiber bundles `E‚ÇÅ`, `E‚ÇÇ` over a base `B`, the forward
function for the construction `Trivialization.prod`, the induced
trivialization for the fiberwise product of `E‚ÇÅ` and `E‚ÇÇ`. -/
def Prod.toFun' : TotalSpace (F‚ÇÅ √ó F‚ÇÇ) (E‚ÇÅ √ó·µá E‚ÇÇ) ‚Üí B √ó F‚ÇÅ √ó F‚ÇÇ :=
  fun p ‚Ü¶ ‚ü®p.1, (e‚ÇÅ ‚ü®p.1, p.2.1‚ü©).2, (e‚ÇÇ ‚ü®p.1, p.2.2‚ü©).2‚ü©

variable {e‚ÇÅ e‚ÇÇ}

theorem Prod.continuous_to_fun : ContinuousOn (Prod.toFun' e‚ÇÅ e‚ÇÇ)
    (œÄ (F‚ÇÅ √ó F‚ÇÇ) (E‚ÇÅ √ó·µá E‚ÇÇ) ‚Åª¬π' (e‚ÇÅ.baseSet ‚à© e‚ÇÇ.baseSet)) := by
  let f‚ÇÅ : TotalSpace (F‚ÇÅ √ó F‚ÇÇ) (E‚ÇÅ √ó·µá E‚ÇÇ) ‚Üí TotalSpace F‚ÇÅ E‚ÇÅ √ó TotalSpace F‚ÇÇ E‚ÇÇ :=
    fun p ‚Ü¶ ((‚ü®p.1, p.2.1‚ü© : TotalSpace F‚ÇÅ E‚ÇÅ), (‚ü®p.1, p.2.2‚ü© : TotalSpace F‚ÇÇ E‚ÇÇ))
  let f‚ÇÇ : TotalSpace F‚ÇÅ E‚ÇÅ √ó TotalSpace F‚ÇÇ E‚ÇÇ ‚Üí (B √ó F‚ÇÅ) √ó B √ó F‚ÇÇ := fun p ‚Ü¶ ‚ü®e‚ÇÅ p.1, e‚ÇÇ p.2‚ü©
  let f‚ÇÉ : (B √ó F‚ÇÅ) √ó B √ó F‚ÇÇ ‚Üí B √ó F‚ÇÅ √ó F‚ÇÇ := fun p ‚Ü¶ ‚ü®p.1.1, p.1.2, p.2.2‚ü©
  have hf‚ÇÅ : Continuous f‚ÇÅ := (Prod.isInducing_diag F‚ÇÅ E‚ÇÅ F‚ÇÇ E‚ÇÇ).continuous
  have hf‚ÇÇ : ContinuousOn f‚ÇÇ (e‚ÇÅ.source √óÀ¢ e‚ÇÇ.source) :=
    e‚ÇÅ.toPartialHomeomorph.continuousOn.prodMap e‚ÇÇ.toPartialHomeomorph.continuousOn
  have hf‚ÇÉ : Continuous f‚ÇÉ := by fun_prop
  refine ((hf‚ÇÉ.comp_continuousOn hf‚ÇÇ).comp hf‚ÇÅ.continuousOn ?_).congr ?_
  ¬∑ rw [e‚ÇÅ.source_eq, e‚ÇÇ.source_eq]
    exact mapsTo_preimage _ _
  rintro ‚ü®b, v‚ÇÅ, v‚ÇÇ‚ü© ‚ü®hb‚ÇÅ, _‚ü©
  simp only [f‚ÇÅ, f‚ÇÇ, f‚ÇÉ, Prod.toFun', Prod.mk_inj, Function.comp_apply, and_true]
  rw [e‚ÇÅ.coe_fst]
  rw [e‚ÇÅ.source_eq, mem_preimage]
  exact hb‚ÇÅ

variable (e‚ÇÅ e‚ÇÇ) [‚àÄ x, Zero (E‚ÇÅ x)] [‚àÄ x, Zero (E‚ÇÇ x)]

/-- Given trivializations `e‚ÇÅ`, `e‚ÇÇ` for fiber bundles `E‚ÇÅ`, `E‚ÇÇ` over a base `B`, the inverse
function for the construction `Trivialization.prod`, the induced
trivialization for the fiberwise product of `E‚ÇÅ` and `E‚ÇÇ`. -/
noncomputable def Prod.invFun' (p : B √ó F‚ÇÅ √ó F‚ÇÇ) : TotalSpace (F‚ÇÅ √ó F‚ÇÇ) (E‚ÇÅ √ó·µá E‚ÇÇ) :=
  ‚ü®p.1, e‚ÇÅ.symm p.1 p.2.1, e‚ÇÇ.symm p.1 p.2.2‚ü©

variable {e‚ÇÅ e‚ÇÇ}

theorem Prod.left_inv {x : TotalSpace (F‚ÇÅ √ó F‚ÇÇ) (E‚ÇÅ √ó·µá E‚ÇÇ)}
    (h : x ‚àà œÄ (F‚ÇÅ √ó F‚ÇÇ) (E‚ÇÅ √ó·µá E‚ÇÇ) ‚Åª¬π' (e‚ÇÅ.baseSet ‚à© e‚ÇÇ.baseSet)) :
    Prod.invFun' e‚ÇÅ e‚ÇÇ (Prod.toFun' e‚ÇÅ e‚ÇÇ x) = x := by
  obtain ‚ü®x, v‚ÇÅ, v‚ÇÇ‚ü© := x
  obtain ‚ü®h‚ÇÅ : x ‚àà e‚ÇÅ.baseSet, h‚ÇÇ : x ‚àà e‚ÇÇ.baseSet‚ü© := h
  simp only [Prod.toFun', Prod.invFun', symm_apply_apply_mk, h‚ÇÅ, h‚ÇÇ]

theorem Prod.right_inv {x : B √ó F‚ÇÅ √ó F‚ÇÇ}
    (h : x ‚àà (e‚ÇÅ.baseSet ‚à© e‚ÇÇ.baseSet) √óÀ¢ (univ : Set (F‚ÇÅ √ó F‚ÇÇ))) :
    Prod.toFun' e‚ÇÅ e‚ÇÇ (Prod.invFun' e‚ÇÅ e‚ÇÇ x) = x := by
  obtain ‚ü®x, w‚ÇÅ, w‚ÇÇ‚ü© := x
  obtain ‚ü®‚ü®h‚ÇÅ : x ‚àà e‚ÇÅ.baseSet, h‚ÇÇ : x ‚àà e‚ÇÇ.baseSet‚ü©, -‚ü© := h
  simp only [Prod.toFun', Prod.invFun', apply_mk_symm, h‚ÇÅ, h‚ÇÇ]

theorem Prod.continuous_inv_fun :
    ContinuousOn (Prod.invFun' e‚ÇÅ e‚ÇÇ) ((e‚ÇÅ.baseSet ‚à© e‚ÇÇ.baseSet) √óÀ¢ univ) := by
  rw [(Prod.isInducing_diag F‚ÇÅ E‚ÇÅ F‚ÇÇ E‚ÇÇ).continuousOn_iff]
  have H‚ÇÅ : Continuous fun p : B √ó F‚ÇÅ √ó F‚ÇÇ ‚Ü¶ ((p.1, p.2.1), (p.1, p.2.2)) := by fun_prop
  refine (e‚ÇÅ.continuousOn_symm.prodMap e‚ÇÇ.continuousOn_symm).comp H‚ÇÅ.continuousOn ?_
  exact fun x h ‚Ü¶ ‚ü®‚ü®h.1.1, mem_univ _‚ü©, ‚ü®h.1.2, mem_univ _‚ü©‚ü©

variable (e‚ÇÅ e‚ÇÇ)

/-- Given trivializations `e‚ÇÅ`, `e‚ÇÇ` for bundle types `E‚ÇÅ`, `E‚ÇÇ` over a base `B`, the induced
trivialization for the fiberwise product of `E‚ÇÅ` and `E‚ÇÇ`, whose base set is
`e‚ÇÅ.baseSet ‚à© e‚ÇÇ.baseSet`. -/
noncomputable def prod : Trivialization (F‚ÇÅ √ó F‚ÇÇ) (œÄ (F‚ÇÅ √ó F‚ÇÇ) (E‚ÇÅ √ó·µá E‚ÇÇ)) where
  toFun := Prod.toFun' e‚ÇÅ e‚ÇÇ
  invFun := Prod.invFun' e‚ÇÅ e‚ÇÇ
  source := œÄ (F‚ÇÅ √ó F‚ÇÇ) (E‚ÇÅ √ó·µá E‚ÇÇ) ‚Åª¬π' (e‚ÇÅ.baseSet ‚à© e‚ÇÇ.baseSet)
  target := (e‚ÇÅ.baseSet ‚à© e‚ÇÇ.baseSet) √óÀ¢ Set.univ
  map_source' _ h := ‚ü®h, Set.mem_univ _‚ü©
  map_target' _ h := h.1
  left_inv' _ := Prod.left_inv
  right_inv' _ := Prod.right_inv
  open_source := by
    convert (e‚ÇÅ.open_source.prod e‚ÇÇ.open_source).preimage
        (FiberBundle.Prod.isInducing_diag F‚ÇÅ E‚ÇÅ F‚ÇÇ E‚ÇÇ).continuous
    ext x
    simp only [Trivialization.source_eq, mfld_simps]
  open_target := (e‚ÇÅ.open_baseSet.inter e‚ÇÇ.open_baseSet).prod isOpen_univ
  continuousOn_toFun := Prod.continuous_to_fun
  continuousOn_invFun := Prod.continuous_inv_fun
  baseSet := e‚ÇÅ.baseSet ‚à© e‚ÇÇ.baseSet
  open_baseSet := e‚ÇÅ.open_baseSet.inter e‚ÇÇ.open_baseSet
  source_eq := rfl
  target_eq := rfl
  proj_toFun _ _ := rfl

@[simp]
theorem baseSet_prod : (prod e‚ÇÅ e‚ÇÇ).baseSet = e‚ÇÅ.baseSet ‚à© e‚ÇÇ.baseSet := rfl

theorem prod_symm_apply (x : B) (w‚ÇÅ : F‚ÇÅ) (w‚ÇÇ : F‚ÇÇ) :
    (prod e‚ÇÅ e‚ÇÇ).toPartialEquiv.symm (x, w‚ÇÅ, w‚ÇÇ) = ‚ü®x, e‚ÇÅ.symm x w‚ÇÅ, e‚ÇÇ.symm x w‚ÇÇ‚ü© := rfl

theorem prod_symm_apply' (x : B) (w‚ÇÅ : F‚ÇÅ) (w‚ÇÇ : F‚ÇÇ) :
    (prod e‚ÇÅ e‚ÇÇ).toPartialHomeomorph.symm (x, w‚ÇÅ, w‚ÇÇ) = ‚ü®x, e‚ÇÅ.symm x w‚ÇÅ, e‚ÇÇ.symm x w‚ÇÇ‚ü© := rfl

@[simp]
theorem fst_prod_symm_apply (x : B) (w‚ÇÅ : F‚ÇÅ) (w‚ÇÇ : F‚ÇÇ) (hx : x ‚àà e‚ÇÅ.baseSet) :
    TotalSpace.Prod.fst F‚ÇÅ F‚ÇÇ E‚ÇÅ E‚ÇÇ ((prod e‚ÇÅ e‚ÇÇ).toPartialHomeomorph.symm (x, w‚ÇÅ, w‚ÇÇ)) =
    e‚ÇÅ.toPartialHomeomorph.symm (x, w‚ÇÅ) := by
  simp only [TotalSpace.Prod.fst]
  ext
  ¬∑ simp [symm_coe_proj e‚ÇÅ hx, prod_symm_apply']
  ¬∑ simp [prod_symm_apply', Trivialization.symm_apply _ hx]

@[simp]
theorem snd_prod_symm_apply (x : B) (w‚ÇÅ : F‚ÇÅ) (w‚ÇÇ : F‚ÇÇ) (hx : x ‚àà e‚ÇÇ.baseSet) :
    TotalSpace.Prod.snd F‚ÇÅ F‚ÇÇ E‚ÇÅ E‚ÇÇ ((prod e‚ÇÅ e‚ÇÇ).toPartialHomeomorph.symm (x, w‚ÇÅ, w‚ÇÇ)) =
    e‚ÇÇ.toPartialHomeomorph.symm (x, w‚ÇÇ) := by
  simp only [TotalSpace.Prod.snd]
  ext
  ¬∑ simp [symm_coe_proj e‚ÇÇ hx, prod_symm_apply']
  ¬∑ simp [prod_symm_apply', Trivialization.symm_apply _ hx]

end Trivialization

open Trivialization

variable [‚àÄ x, Zero (E‚ÇÅ x)] [‚àÄ x, Zero (E‚ÇÇ x)] [‚àÄ x : B, TopologicalSpace (E‚ÇÅ x)]
  [‚àÄ x : B, TopologicalSpace (E‚ÇÇ x)] [FiberBundle F‚ÇÅ E‚ÇÅ] [FiberBundle F‚ÇÇ E‚ÇÇ]

/-- The product of two fiber bundles is a fiber bundle. -/
noncomputable instance FiberBundle.prod : FiberBundle (F‚ÇÅ √ó F‚ÇÇ) (E‚ÇÅ √ó·µá E‚ÇÇ) where
  totalSpaceMk_isInducing' b := by
    rw [‚Üê (Prod.isInducing_diag F‚ÇÅ E‚ÇÅ F‚ÇÇ E‚ÇÇ).of_comp_iff]
    exact (totalSpaceMk_isInducing F‚ÇÅ E‚ÇÅ b).prodMap (totalSpaceMk_isInducing F‚ÇÇ E‚ÇÇ b)
  trivializationAtlas' := { e |
    ‚àÉ (e‚ÇÅ : Trivialization F‚ÇÅ (œÄ F‚ÇÅ E‚ÇÅ)) (e‚ÇÇ : Trivialization F‚ÇÇ (œÄ F‚ÇÇ E‚ÇÇ))
      (_ : MemTrivializationAtlas e‚ÇÅ) (_ : MemTrivializationAtlas e‚ÇÇ),
      e = Trivialization.prod e‚ÇÅ e‚ÇÇ }
  trivializationAt' b := (trivializationAt F‚ÇÅ E‚ÇÅ b).prod (trivializationAt F‚ÇÇ E‚ÇÇ b)
  mem_baseSet_trivializationAt' b :=
    ‚ü®mem_baseSet_trivializationAt F‚ÇÅ E‚ÇÅ b, mem_baseSet_trivializationAt F‚ÇÇ E‚ÇÇ b‚ü©
  trivialization_mem_atlas' b :=
    ‚ü®trivializationAt F‚ÇÅ E‚ÇÅ b, trivializationAt F‚ÇÇ E‚ÇÇ b, inferInstance, inferInstance, rfl‚ü©

instance {e‚ÇÅ : Trivialization F‚ÇÅ (œÄ F‚ÇÅ E‚ÇÅ)} {e‚ÇÇ : Trivialization F‚ÇÇ (œÄ F‚ÇÇ E‚ÇÇ)}
    [MemTrivializationAtlas e‚ÇÅ] [MemTrivializationAtlas e‚ÇÇ] :
    MemTrivializationAtlas (e‚ÇÅ.prod e‚ÇÇ : Trivialization (F‚ÇÅ √ó F‚ÇÇ) (œÄ (F‚ÇÅ √ó F‚ÇÇ) (E‚ÇÅ √ó·µá E‚ÇÇ))) where
  out := ‚ü®e‚ÇÅ, e‚ÇÇ, inferInstance, inferInstance, rfl‚ü©

end Prod

/-! ### Pullbacks of fiber bundles -/

section

universe u v w‚ÇÅ w‚ÇÇ U

variable {B : Type u} (F : Type v) (E : B ‚Üí Type w‚ÇÅ) {B' : Type w‚ÇÇ} (f : B' ‚Üí B)

instance [‚àÄ x : B, TopologicalSpace (E x)] : ‚àÄ x : B', TopologicalSpace ((f *·µñ E) x) :=
  inferInstanceAs (‚àÄ x, TopologicalSpace (E (f x)))

variable [TopologicalSpace B'] [TopologicalSpace (TotalSpace F E)]

/-- Definition of `Pullback.TotalSpace.topologicalSpace`, which we make irreducible. -/
irreducible_def pullbackTopology : TopologicalSpace (TotalSpace F (f *·µñ E)) :=
  induced TotalSpace.proj ‚ÄπTopologicalSpace B'‚Ä∫ ‚äì
    induced (Pullback.lift f) ‚ÄπTopologicalSpace (TotalSpace F E)‚Ä∫

/-- The topology on the total space of a pullback bundle is the coarsest topology for which both
the projections to the base and the map to the original bundle are continuous. -/
instance Pullback.TotalSpace.topologicalSpace : TopologicalSpace (TotalSpace F (f *·µñ E)) :=
  pullbackTopology F E f

theorem Pullback.continuous_proj (f : B' ‚Üí B) : Continuous (œÄ F (f *·µñ E)) := by
  rw [continuous_iff_le_induced, Pullback.TotalSpace.topologicalSpace, pullbackTopology_def]
  exact inf_le_left

theorem Pullback.continuous_lift (f : B' ‚Üí B) : Continuous (@Pullback.lift B F E B' f) := by
  rw [continuous_iff_le_induced, Pullback.TotalSpace.topologicalSpace, pullbackTopology_def]
  exact inf_le_right

theorem inducing_pullbackTotalSpaceEmbedding (f : B' ‚Üí B) :
    IsInducing (@pullbackTotalSpaceEmbedding B F E B' f) := by
  constructor
  simp_rw [instTopologicalSpaceProd, induced_inf, induced_compose,
    Pullback.TotalSpace.topologicalSpace, pullbackTopology_def]
  rfl

variable {F E} in
/-- Given a fiber bundle `E` over a manifold `B` and a continuous map `f : B' ‚Üí B`, if `œÜ` is
a map into the total space of the pullback `f *·µñ E`, then its continuity can be checked by checking
the continuity of (1) the map `TotalSpace.proj ‚àò œÜ` into `B'`, and (ii) the map
`Pullback.lift f ‚àò œÜ` into the total space of `E`. -/
theorem Pullback.continuous_of_continuous_proj_comp_of_continuous_lift_comp
    (f : B' ‚Üí B) {M : Type*} [TopologicalSpace M] {œÜ : M ‚Üí TotalSpace F (f *·µñ E)}
    (h1 : Continuous (TotalSpace.proj ‚àò œÜ)) (h2 : Continuous (Pullback.lift f ‚àò œÜ)) :
    Continuous œÜ := by
  rw [(inducing_pullbackTotalSpaceEmbedding F E f).continuous_iff]
  apply h1.prod_mk h2

variable {F E} in
/-- Given a fiber bundle `E` over a manifold `B` and a continuous map `f : B' ‚Üí B`, if `œÜ` is
a map into the total space of the pullback `f *·µñ E`, then its continuity can be checked by checking
the continuity of (1) the map `TotalSpace.proj ‚àò œÜ` into `B'`, and (ii) the map
`Pullback.lift f ‚àò œÜ` into the total space of `E`. -/
theorem Pullback.continuous_iff_continuous_proj_comp_and_continuous_lift_comp
    (f : B' ‚Üí B) {M : Type*} [TopologicalSpace M] (œÜ : M ‚Üí TotalSpace F (f *·µñ E)) :
    Continuous œÜ ‚Üî (Continuous (TotalSpace.proj ‚àò œÜ) ‚àß Continuous (Pullback.lift f ‚àò œÜ)) := by
  refine ‚ü®fun h ‚Ü¶ ‚ü®?_, ?_‚ü©, fun ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚Ü¶ ?_‚ü©
  ¬∑ exact (continuous_proj F E _).comp h
  ¬∑ exact (Pullback.continuous_lift F E _).comp h
  ¬∑ exact Pullback.continuous_of_continuous_proj_comp_of_continuous_lift_comp _ h‚ÇÅ h‚ÇÇ

section FiberBundle

variable [TopologicalSpace F] [TopologicalSpace B]

theorem Pullback.continuous_totalSpaceMk [‚àÄ x, TopologicalSpace (E x)] [FiberBundle F E]
    {f : B' ‚Üí B} {x : B'} : Continuous (@TotalSpace.mk _ F (f *·µñ E) x) := by
  simp only [continuous_iff_le_induced, Pullback.TotalSpace.topologicalSpace, induced_compose,
    induced_inf, Function.comp_def, induced_const, top_inf_eq, pullbackTopology_def]
  exact (FiberBundle.totalSpaceMk_isInducing F E (f x)).eq_induced.le

variable {E F}
variable [‚àÄ _b, Zero (E _b)] {K : Type U} [FunLike K B' B] [ContinuousMapClass K B' B]

/-- A fiber bundle trivialization can be pulled back to a trivialization on the pullback bundle. -/
noncomputable def Trivialization.pullback (e : Trivialization F (œÄ F E)) (f : K) :
    Trivialization F (œÄ F ((f : B' ‚Üí B) *·µñ E)) where
  toFun z := (z.proj, (e (Pullback.lift f z)).2)
  invFun y := @TotalSpace.mk _ F (f *·µñ E) y.1 (e.symm (f y.1) y.2)
  source := Pullback.lift f ‚Åª¬π' e.source
  baseSet := f ‚Åª¬π' e.baseSet
  target := (f ‚Åª¬π' e.baseSet) √óÀ¢ univ
  map_source' x h := by
    simp_rw [e.source_eq, mem_preimage, Pullback.lift_proj] at h
    simp_rw [prodMk_mem_set_prod_eq, mem_univ, and_true, mem_preimage, h]
  map_target' y h := by
    rw [mem_prod, mem_preimage] at h
    simp_rw [e.source_eq, mem_preimage, Pullback.lift_proj, h.1]
  left_inv' x h := by
    simp_rw [mem_preimage, e.mem_source, Pullback.lift_proj] at h
    simp_rw [Pullback.lift, e.symm_apply_apply_mk h]
  right_inv' x h := by
    simp_rw [mem_prod, mem_preimage, mem_univ, and_true] at h
    simp_rw [Pullback.lift_mk, e.apply_mk_symm h]
  open_source := by
    simp_rw [e.source_eq, ‚Üê preimage_comp]
    exact e.open_baseSet.preimage ((map_continuous f).comp <| Pullback.continuous_proj F E f)
  open_target := ((map_continuous f).isOpen_preimage _ e.open_baseSet).prod isOpen_univ
  open_baseSet := (map_continuous f).isOpen_preimage _ e.open_baseSet
  continuousOn_toFun :=
    (Pullback.continuous_proj F E f).continuousOn.prodMk
      (continuous_snd.comp_continuousOn <|
        e.continuousOn.comp (Pullback.continuous_lift F E f).continuousOn Subset.rfl)
  continuousOn_invFun := by
    simp_rw [(inducing_pullbackTotalSpaceEmbedding F E f).continuousOn_iff, Function.comp_def,
      pullbackTotalSpaceEmbedding]
    exact continuousOn_fst.prodMk
      (e.continuousOn_symm.comp ((map_continuous f).prodMap continuous_id).continuousOn Subset.rfl)
  source_eq := by
    rw [e.source_eq]
    rfl
  target_eq := rfl
  proj_toFun _ _ := rfl

noncomputable instance FiberBundle.pullback [‚àÄ x, TopologicalSpace (E x)] [FiberBundle F E]
    (f : K) : FiberBundle F ((f : B' ‚Üí B) *·µñ E) where
  totalSpaceMk_isInducing' x :=
    (totalSpaceMk_isInducing F E (f x)).of_comp (Pullback.continuous_totalSpaceMk F E)
      (Pullback.continuous_lift F E f)
  trivializationAtlas' :=
    { ef | ‚àÉ (e : Trivialization F (œÄ F E)) (_ : MemTrivializationAtlas e), ef = e.pullback f }
  trivializationAt' x := (trivializationAt F E (f x)).pullback f
  mem_baseSet_trivializationAt' x := mem_baseSet_trivializationAt F E (f x)
  trivialization_mem_atlas' x := ‚ü®trivializationAt F E (f x), inferInstance, rfl‚ü©

@[simp] lemma lift_pullback_symm_apply (f : K) (e : Trivialization F (œÄ F E))
    {x : B'} {v : F} (hx : f x ‚àà e.baseSet) :
    Pullback.lift f ((e.pullback f).toPartialHomeomorph.symm (x, v)) =
      e.toPartialHomeomorph.symm (f x, v) := by
  have A : (e.toPartialHomeomorph.symm (f x, v)).proj = f x := Trivialization.symm_coe_proj e hx
  simp only [Pullback.lift, Trivialization.pullback, PartialHomeomorph.mk_coe_symm,
    PartialEquiv.coe_symm_mk]
  ext
  ¬∑ simp [A]
  ¬∑ simp [A, Trivialization.symm_apply _ hx]

end FiberBundle

end
