/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Data.Bundle
import Mathlib.Data.Set.Image
import Mathlib.Topology.PartialHomeomorph
import Mathlib.Topology.Order.Basic

#align_import topology.fiber_bundle.trivialization from "leanprover-community/mathlib"@"e473c3198bb41f68560cab68a0529c854b618833"

/-!
# Trivializations

## Main definitions

### Basic definitions

* `Trivialization F p` : structure extending partial homeomorphisms, defining a local
  trivialization of a topological space `Z` with projection `p` and fiber `F`.

* `Pretrivialization F proj` : trivialization as a partial equivalence, mainly used when the
  topology on the total space has not yet been defined.

### Operations on bundles

We provide the following operations on `Trivialization`s.

* `Trivialization.compHomeomorph`: given a local trivialization `e` of a fiber bundle
  `p : Z ‚Üí B` and a homeomorphism `h : Z' ‚âÉ‚Çú Z`, returns a local trivialization of the fiber bundle
  `p ‚àò h`.

## Implementation notes

Previously, in mathlib, there was a structure `topological_vector_bundle.trivialization` which
extended another structure `topological_fiber_bundle.trivialization` by a linearity hypothesis. As
of PR leanprover-community/mathlib#17359, we have changed this to a single structure
`Trivialization` (no namespace), together with a mixin class `Trivialization.IsLinear`.

This permits all the *data* of a vector bundle to be held at the level of fiber bundles, so that the
same trivializations can underlie an object's structure as (say) a vector bundle over `‚ÑÇ` and as a
vector bundle over `‚Ñù`, as well as its structure simply as a fiber bundle.

This might be a little surprising, given the general trend of the library to ever-increased
bundling.  But in this case the typical motivation for more bundling does not apply: there is no
algebraic or order structure on the whole type of linear (say) trivializations of a bundle.
Indeed, since trivializations only have meaning on their base sets (taking junk values outside), the
type of linear trivializations is not even particularly well-behaved.
-/

open TopologicalSpace Filter Set Bundle Function

open scoped Topology Classical Bundle

variable {Œπ : Type*} {B : Type*} {F : Type*} {E : B ‚Üí Type*}
variable (F) {Z : Type*} [TopologicalSpace B] [TopologicalSpace F] {proj : Z ‚Üí B}

/-- This structure contains the information left for a local trivialization (which is implemented
below as `Trivialization F proj`) if the total space has not been given a topology, but we
have a topology on both the fiber and the base space. Through the construction
`topological_fiber_prebundle F proj` it will be possible to promote a
`Pretrivialization F proj` to a `Trivialization F proj`. -/
structure Pretrivialization (proj : Z ‚Üí B) extends PartialEquiv Z (B √ó F) where
  open_target : IsOpen target
  baseSet : Set B
  open_baseSet : IsOpen baseSet
  source_eq : source = proj ‚Åª¬π' baseSet
  target_eq : target = baseSet √óÀ¢ univ
  proj_toFun : ‚àÄ p ‚àà source, (toFun p).1 = proj p
#align pretrivialization Pretrivialization

namespace Pretrivialization

variable {F}
variable (e : Pretrivialization F proj) {x : Z}

/-- Coercion of a pretrivialization to a function. We don't use `e.toFun` in the `CoeFun` instance
because it is actually `e.toPartialEquiv.toFun`, so `simp` will apply lemmas about
`toPartialEquiv`. While we may want to switch to this behavior later, doing it mid-port will break a
lot of proofs.  -/
@[coe] def toFun' : Z ‚Üí (B √ó F) := e.toFun

instance : CoeFun (Pretrivialization F proj) fun _ => Z ‚Üí B √ó F := ‚ü®toFun'‚ü©

@[ext]
lemma ext' (e e' : Pretrivialization F proj) (h‚ÇÅ : e.toPartialEquiv = e'.toPartialEquiv)
    (h‚ÇÇ : e.baseSet = e'.baseSet) : e = e' := by
  cases e; cases e'; congr
#align pretrivialization.ext Pretrivialization.ext'

-- Porting note (#11215): TODO: move `ext` here?
lemma ext {e e' : Pretrivialization F proj} (h‚ÇÅ : ‚àÄ x, e x = e' x)
    (h‚ÇÇ : ‚àÄ x, e.toPartialEquiv.symm x = e'.toPartialEquiv.symm x) (h‚ÇÉ : e.baseSet = e'.baseSet) :
    e = e' := by
  ext1 <;> [ext1; exact h‚ÇÉ]
  ¬∑ apply h‚ÇÅ
  ¬∑ apply h‚ÇÇ
  ¬∑ rw [e.source_eq, e'.source_eq, h‚ÇÉ]

/-- If the fiber is nonempty, then the projection also is. -/
lemma toPartialEquiv_injective [Nonempty F] :
    Injective (toPartialEquiv : Pretrivialization F proj ‚Üí PartialEquiv Z (B √ó F)) := by
  refine fun e e' h ‚Ü¶ ext' _ _ h ?_
  simpa only [fst_image_prod, univ_nonempty, target_eq]
    using congr_arg (Prod.fst '' PartialEquiv.target ¬∑) h

@[simp, mfld_simps]
theorem coe_coe : ‚áëe.toPartialEquiv = e :=
  rfl
#align pretrivialization.coe_coe Pretrivialization.coe_coe

@[simp, mfld_simps]
theorem coe_fst (ex : x ‚àà e.source) : (e x).1 = proj x :=
  e.proj_toFun x ex
#align pretrivialization.coe_fst Pretrivialization.coe_fst

theorem mem_source : x ‚àà e.source ‚Üî proj x ‚àà e.baseSet := by rw [e.source_eq, mem_preimage]
#align pretrivialization.mem_source Pretrivialization.mem_source

theorem coe_fst' (ex : proj x ‚àà e.baseSet) : (e x).1 = proj x :=
  e.coe_fst (e.mem_source.2 ex)
#align pretrivialization.coe_fst' Pretrivialization.coe_fst'

protected theorem eqOn : EqOn (Prod.fst ‚àò e) proj e.source := fun _ hx => e.coe_fst hx
#align pretrivialization.eq_on Pretrivialization.eqOn

theorem mk_proj_snd (ex : x ‚àà e.source) : (proj x, (e x).2) = e x :=
  Prod.ext (e.coe_fst ex).symm rfl
#align pretrivialization.mk_proj_snd Pretrivialization.mk_proj_snd

theorem mk_proj_snd' (ex : proj x ‚àà e.baseSet) : (proj x, (e x).2) = e x :=
  Prod.ext (e.coe_fst' ex).symm rfl
#align pretrivialization.mk_proj_snd' Pretrivialization.mk_proj_snd'

/-- Composition of inverse and coercion from the subtype of the target. -/
def setSymm : e.target ‚Üí Z :=
  e.target.restrict e.toPartialEquiv.symm
#align pretrivialization.set_symm Pretrivialization.setSymm

theorem mem_target {x : B √ó F} : x ‚àà e.target ‚Üî x.1 ‚àà e.baseSet := by
  rw [e.target_eq, prod_univ, mem_preimage]
#align pretrivialization.mem_target Pretrivialization.mem_target

theorem proj_symm_apply {x : B √ó F} (hx : x ‚àà e.target) : proj (e.toPartialEquiv.symm x) = x.1 := by
  have := (e.coe_fst (e.map_target hx)).symm
  rwa [‚Üê e.coe_coe, e.right_inv hx] at this
#align pretrivialization.proj_symm_apply Pretrivialization.proj_symm_apply

theorem proj_symm_apply' {b : B} {x : F} (hx : b ‚àà e.baseSet) :
    proj (e.toPartialEquiv.symm (b, x)) = b :=
  e.proj_symm_apply (e.mem_target.2 hx)
#align pretrivialization.proj_symm_apply' Pretrivialization.proj_symm_apply'

theorem proj_surjOn_baseSet [Nonempty F] : Set.SurjOn proj e.source e.baseSet := fun b hb =>
  let ‚ü®y‚ü© := ‚ÄπNonempty F‚Ä∫
  ‚ü®e.toPartialEquiv.symm (b, y), e.toPartialEquiv.map_target <| e.mem_target.2 hb,
    e.proj_symm_apply' hb‚ü©
#align pretrivialization.proj_surj_on_base_set Pretrivialization.proj_surjOn_baseSet

theorem apply_symm_apply {x : B √ó F} (hx : x ‚àà e.target) : e (e.toPartialEquiv.symm x) = x :=
  e.toPartialEquiv.right_inv hx
#align pretrivialization.apply_symm_apply Pretrivialization.apply_symm_apply

theorem apply_symm_apply' {b : B} {x : F} (hx : b ‚àà e.baseSet) :
    e (e.toPartialEquiv.symm (b, x)) = (b, x) :=
  e.apply_symm_apply (e.mem_target.2 hx)
#align pretrivialization.apply_symm_apply' Pretrivialization.apply_symm_apply'

theorem symm_apply_apply {x : Z} (hx : x ‚àà e.source) : e.toPartialEquiv.symm (e x) = x :=
  e.toPartialEquiv.left_inv hx
#align pretrivialization.symm_apply_apply Pretrivialization.symm_apply_apply

@[simp, mfld_simps]
theorem symm_apply_mk_proj {x : Z} (ex : x ‚àà e.source) :
    e.toPartialEquiv.symm (proj x, (e x).2) = x := by
  rw [‚Üê e.coe_fst ex, ‚Üê e.coe_coe, e.left_inv ex]
#align pretrivialization.symm_apply_mk_proj Pretrivialization.symm_apply_mk_proj

@[simp, mfld_simps]
theorem preimage_symm_proj_baseSet :
    e.toPartialEquiv.symm ‚Åª¬π' (proj ‚Åª¬π' e.baseSet) ‚à© e.target = e.target := by
  refine inter_eq_right.mpr fun x hx => ?_
  simp only [mem_preimage, PartialEquiv.invFun_as_coe, e.proj_symm_apply hx]
  exact e.mem_target.mp hx
#align pretrivialization.preimage_symm_proj_base_set Pretrivialization.preimage_symm_proj_baseSet

@[simp, mfld_simps]
theorem preimage_symm_proj_inter (s : Set B) :
    e.toPartialEquiv.symm ‚Åª¬π' (proj ‚Åª¬π' s) ‚à© e.baseSet √óÀ¢ univ = (s ‚à© e.baseSet) √óÀ¢ univ := by
  ext ‚ü®x, y‚ü©
  suffices x ‚àà e.baseSet ‚Üí (proj (e.toPartialEquiv.symm (x, y)) ‚àà s ‚Üî x ‚àà s) by
    simpa only [prod_mk_mem_set_prod_eq, mem_inter_iff, and_true_iff, mem_univ, and_congr_left_iff]
  intro h
  rw [e.proj_symm_apply' h]
#align pretrivialization.preimage_symm_proj_inter Pretrivialization.preimage_symm_proj_inter

theorem target_inter_preimage_symm_source_eq (e f : Pretrivialization F proj) :
    f.target ‚à© f.toPartialEquiv.symm ‚Åª¬π' e.source = (e.baseSet ‚à© f.baseSet) √óÀ¢ univ := by
  rw [inter_comm, f.target_eq, e.source_eq, f.preimage_symm_proj_inter]
#align pretrivialization.target_inter_preimage_symm_source_eq Pretrivialization.target_inter_preimage_symm_source_eq

theorem trans_source (e f : Pretrivialization F proj) :
    (f.toPartialEquiv.symm.trans e.toPartialEquiv).source = (e.baseSet ‚à© f.baseSet) √óÀ¢ univ := by
  rw [PartialEquiv.trans_source, PartialEquiv.symm_source, e.target_inter_preimage_symm_source_eq]
#align pretrivialization.trans_source Pretrivialization.trans_source

theorem symm_trans_symm (e e' : Pretrivialization F proj) :
    (e.toPartialEquiv.symm.trans e'.toPartialEquiv).symm
      = e'.toPartialEquiv.symm.trans e.toPartialEquiv := by
  rw [PartialEquiv.trans_symm_eq_symm_trans_symm, PartialEquiv.symm_symm]
#align pretrivialization.symm_trans_symm Pretrivialization.symm_trans_symm

theorem symm_trans_source_eq (e e' : Pretrivialization F proj) :
    (e.toPartialEquiv.symm.trans e'.toPartialEquiv).source = (e.baseSet ‚à© e'.baseSet) √óÀ¢ univ := by
  rw [PartialEquiv.trans_source, e'.source_eq, PartialEquiv.symm_source, e.target_eq, inter_comm,
    e.preimage_symm_proj_inter, inter_comm]
#align pretrivialization.symm_trans_source_eq Pretrivialization.symm_trans_source_eq

theorem symm_trans_target_eq (e e' : Pretrivialization F proj) :
    (e.toPartialEquiv.symm.trans e'.toPartialEquiv).target = (e.baseSet ‚à© e'.baseSet) √óÀ¢ univ := by
  rw [‚Üê PartialEquiv.symm_source, symm_trans_symm, symm_trans_source_eq, inter_comm]
#align pretrivialization.symm_trans_target_eq Pretrivialization.symm_trans_target_eq

variable (e' : Pretrivialization F (œÄ F E)) {x' : TotalSpace F E} {b : B} {y : E b}

@[simp]
theorem coe_mem_source : ‚Üëy ‚àà e'.source ‚Üî b ‚àà e'.baseSet :=
  e'.mem_source
#align pretrivialization.coe_mem_source Pretrivialization.coe_mem_source

@[simp, mfld_simps]
theorem coe_coe_fst (hb : b ‚àà e'.baseSet) : (e' y).1 = b :=
  e'.coe_fst (e'.mem_source.2 hb)
#align pretrivialization.coe_coe_fst Pretrivialization.coe_coe_fst

theorem mk_mem_target {x : B} {y : F} : (x, y) ‚àà e'.target ‚Üî x ‚àà e'.baseSet :=
  e'.mem_target
#align pretrivialization.mk_mem_target Pretrivialization.mk_mem_target

theorem symm_coe_proj {x : B} {y : F} (e' : Pretrivialization F (œÄ F E)) (h : x ‚àà e'.baseSet) :
    (e'.toPartialEquiv.symm (x, y)).1 = x :=
  e'.proj_symm_apply' h
#align pretrivialization.symm_coe_proj Pretrivialization.symm_coe_proj

section Zero

variable [‚àÄ x, Zero (E x)]

/-- A fiberwise inverse to `e`. This is the function `F ‚Üí E b` that induces a local inverse
`B √ó F ‚Üí TotalSpace F E` of `e` on `e.baseSet`. It is defined to be `0` outside `e.baseSet`. -/
protected noncomputable def symm (e : Pretrivialization F (œÄ F E)) (b : B) (y : F) : E b :=
  if hb : b ‚àà e.baseSet then
    cast (congr_arg E (e.proj_symm_apply' hb)) (e.toPartialEquiv.symm (b, y)).2
  else 0
#align pretrivialization.symm Pretrivialization.symm

theorem symm_apply (e : Pretrivialization F (œÄ F E)) {b : B} (hb : b ‚àà e.baseSet) (y : F) :
    e.symm b y = cast (congr_arg E (e.symm_coe_proj hb)) (e.toPartialEquiv.symm (b, y)).2 :=
  dif_pos hb
#align pretrivialization.symm_apply Pretrivialization.symm_apply

theorem symm_apply_of_not_mem (e : Pretrivialization F (œÄ F E)) {b : B} (hb : b ‚àâ e.baseSet)
    (y : F) : e.symm b y = 0 :=
  dif_neg hb
#align pretrivialization.symm_apply_of_not_mem Pretrivialization.symm_apply_of_not_mem

theorem coe_symm_of_not_mem (e : Pretrivialization F (œÄ F E)) {b : B} (hb : b ‚àâ e.baseSet) :
    (e.symm b : F ‚Üí E b) = 0 :=
  funext fun _ => dif_neg hb
#align pretrivialization.coe_symm_of_not_mem Pretrivialization.coe_symm_of_not_mem

theorem mk_symm (e : Pretrivialization F (œÄ F E)) {b : B} (hb : b ‚àà e.baseSet) (y : F) :
    TotalSpace.mk b (e.symm b y) = e.toPartialEquiv.symm (b, y) := by
  simp only [e.symm_apply hb, TotalSpace.mk_cast (e.proj_symm_apply' hb), TotalSpace.eta]
#align pretrivialization.mk_symm Pretrivialization.mk_symm

theorem symm_proj_apply (e : Pretrivialization F (œÄ F E)) (z : TotalSpace F E)
    (hz : z.proj ‚àà e.baseSet) : e.symm z.proj (e z).2 = z.2 := by
  rw [e.symm_apply hz, cast_eq_iff_heq, e.mk_proj_snd' hz, e.symm_apply_apply (e.mem_source.mpr hz)]
#align pretrivialization.symm_proj_apply Pretrivialization.symm_proj_apply

theorem symm_apply_apply_mk (e : Pretrivialization F (œÄ F E)) {b : B} (hb : b ‚àà e.baseSet)
    (y : E b) : e.symm b (e ‚ü®b, y‚ü©).2 = y :=
  e.symm_proj_apply ‚ü®b, y‚ü© hb
#align pretrivialization.symm_apply_apply_mk Pretrivialization.symm_apply_apply_mk

theorem apply_mk_symm (e : Pretrivialization F (œÄ F E)) {b : B} (hb : b ‚àà e.baseSet) (y : F) :
    e ‚ü®b, e.symm b y‚ü© = (b, y) := by
  rw [e.mk_symm hb, e.apply_symm_apply (e.mk_mem_target.mpr hb)]
#align pretrivialization.apply_mk_symm Pretrivialization.apply_mk_symm

end Zero

end Pretrivialization

variable [TopologicalSpace Z] [TopologicalSpace (TotalSpace F E)]

/-- A structure extending partial homeomorphisms, defining a local trivialization of a projection
`proj : Z ‚Üí B` with fiber `F`, as a partial homeomorphism between `Z` and `B √ó F` defined between
two sets of the form `proj ‚Åª¬π' baseSet` and `baseSet √ó F`, acting trivially on the first coordinate.
-/
-- Porting note (#5171): was @[nolint has_nonempty_instance]
structure Trivialization (proj : Z ‚Üí B) extends PartialHomeomorph Z (B √ó F) where
  baseSet : Set B
  open_baseSet : IsOpen baseSet
  source_eq : source = proj ‚Åª¬π' baseSet
  target_eq : target = baseSet √óÀ¢ univ
  proj_toFun : ‚àÄ p ‚àà source, (toPartialHomeomorph p).1 = proj p
#align trivialization Trivialization

namespace Trivialization

variable {F}
variable (e : Trivialization F proj) {x : Z}

@[ext]
lemma ext' (e e' : Trivialization F proj) (h‚ÇÅ : e.toPartialHomeomorph = e'.toPartialHomeomorph)
    (h‚ÇÇ : e.baseSet = e'.baseSet) : e = e' := by
  cases e; cases e'; congr
#align trivialization.ext Trivialization.ext'

/-- Coercion of a trivialization to a function. We don't use `e.toFun` in the `CoeFun` instance
because it is actually `e.toPartialEquiv.toFun`, so `simp` will apply lemmas about
`toPartialEquiv`. While we may want to switch to this behavior later, doing it mid-port will break a
lot of proofs.  -/
@[coe] def toFun' : Z ‚Üí (B √ó F) := e.toFun

/-- Natural identification as a `Pretrivialization`. -/
def toPretrivialization : Pretrivialization F proj :=
  { e with }
#align trivialization.to_pretrivialization Trivialization.toPretrivialization

instance : CoeFun (Trivialization F proj) fun _ => Z ‚Üí B √ó F := ‚ü®toFun'‚ü©

instance : Coe (Trivialization F proj) (Pretrivialization F proj) :=
  ‚ü®toPretrivialization‚ü©

theorem toPretrivialization_injective :
    Function.Injective fun e : Trivialization F proj => e.toPretrivialization := fun e e' h => by
  ext1
  exacts [PartialHomeomorph.toPartialEquiv_injective (congr_arg Pretrivialization.toPartialEquiv h),
    congr_arg Pretrivialization.baseSet h]
#align trivialization.to_pretrivialization_injective Trivialization.toPretrivialization_injective

@[simp, mfld_simps]
theorem coe_coe : ‚áëe.toPartialHomeomorph = e :=
  rfl
#align trivialization.coe_coe Trivialization.coe_coe

@[simp, mfld_simps]
theorem coe_fst (ex : x ‚àà e.source) : (e x).1 = proj x :=
  e.proj_toFun x ex
#align trivialization.coe_fst Trivialization.coe_fst

protected theorem eqOn : EqOn (Prod.fst ‚àò e) proj e.source := fun _x hx => e.coe_fst hx
#align trivialization.eq_on Trivialization.eqOn

theorem mem_source : x ‚àà e.source ‚Üî proj x ‚àà e.baseSet := by rw [e.source_eq, mem_preimage]
#align trivialization.mem_source Trivialization.mem_source

theorem coe_fst' (ex : proj x ‚àà e.baseSet) : (e x).1 = proj x :=
  e.coe_fst (e.mem_source.2 ex)
#align trivialization.coe_fst' Trivialization.coe_fst'

theorem mk_proj_snd (ex : x ‚àà e.source) : (proj x, (e x).2) = e x :=
  Prod.ext (e.coe_fst ex).symm rfl
#align trivialization.mk_proj_snd Trivialization.mk_proj_snd

theorem mk_proj_snd' (ex : proj x ‚àà e.baseSet) : (proj x, (e x).2) = e x :=
  Prod.ext (e.coe_fst' ex).symm rfl
#align trivialization.mk_proj_snd' Trivialization.mk_proj_snd'

theorem source_inter_preimage_target_inter (s : Set (B √ó F)) :
    e.source ‚à© e ‚Åª¬π' (e.target ‚à© s) = e.source ‚à© e ‚Åª¬π' s :=
  e.toPartialHomeomorph.source_inter_preimage_target_inter s
#align trivialization.source_inter_preimage_target_inter Trivialization.source_inter_preimage_target_inter

@[simp, mfld_simps]
theorem coe_mk (e : PartialHomeomorph Z (B √ó F)) (i j k l m) (x : Z) :
    (Trivialization.mk e i j k l m : Trivialization F proj) x = e x :=
  rfl
#align trivialization.coe_mk Trivialization.coe_mk

theorem mem_target {x : B √ó F} : x ‚àà e.target ‚Üî x.1 ‚àà e.baseSet :=
  e.toPretrivialization.mem_target
#align trivialization.mem_target Trivialization.mem_target

theorem map_target {x : B √ó F} (hx : x ‚àà e.target) : e.toPartialHomeomorph.symm x ‚àà e.source :=
  e.toPartialHomeomorph.map_target hx
#align trivialization.map_target Trivialization.map_target

theorem proj_symm_apply {x : B √ó F} (hx : x ‚àà e.target) :
    proj (e.toPartialHomeomorph.symm x) = x.1 :=
  e.toPretrivialization.proj_symm_apply hx
#align trivialization.proj_symm_apply Trivialization.proj_symm_apply

theorem proj_symm_apply' {b : B} {x : F} (hx : b ‚àà e.baseSet) :
    proj (e.toPartialHomeomorph.symm (b, x)) = b :=
  e.toPretrivialization.proj_symm_apply' hx
#align trivialization.proj_symm_apply' Trivialization.proj_symm_apply'

theorem proj_surjOn_baseSet [Nonempty F] : Set.SurjOn proj e.source e.baseSet :=
  e.toPretrivialization.proj_surjOn_baseSet
#align trivialization.proj_surj_on_base_set Trivialization.proj_surjOn_baseSet

theorem apply_symm_apply {x : B √ó F} (hx : x ‚àà e.target) : e (e.toPartialHomeomorph.symm x) = x :=
  e.toPartialHomeomorph.right_inv hx
#align trivialization.apply_symm_apply Trivialization.apply_symm_apply

theorem apply_symm_apply' {b : B} {x : F} (hx : b ‚àà e.baseSet) :
    e (e.toPartialHomeomorph.symm (b, x)) = (b, x) :=
  e.toPretrivialization.apply_symm_apply' hx
#align trivialization.apply_symm_apply' Trivialization.apply_symm_apply'

@[simp, mfld_simps]
theorem symm_apply_mk_proj (ex : x ‚àà e.source) : e.toPartialHomeomorph.symm (proj x, (e x).2) = x :=
  e.toPretrivialization.symm_apply_mk_proj ex
#align trivialization.symm_apply_mk_proj Trivialization.symm_apply_mk_proj

theorem symm_trans_source_eq (e e' : Trivialization F proj) :
    (e.toPartialEquiv.symm.trans e'.toPartialEquiv).source = (e.baseSet ‚à© e'.baseSet) √óÀ¢ univ :=
  Pretrivialization.symm_trans_source_eq e.toPretrivialization e'
#align trivialization.symm_trans_source_eq Trivialization.symm_trans_source_eq

theorem symm_trans_target_eq (e e' : Trivialization F proj) :
    (e.toPartialEquiv.symm.trans e'.toPartialEquiv).target = (e.baseSet ‚à© e'.baseSet) √óÀ¢ univ :=
  Pretrivialization.symm_trans_target_eq e.toPretrivialization e'
#align trivialization.symm_trans_target_eq Trivialization.symm_trans_target_eq

theorem coe_fst_eventuallyEq_proj (ex : x ‚àà e.source) : Prod.fst ‚àò e =·∂†[ùìù x] proj :=
  mem_nhds_iff.2 ‚ü®e.source, fun _y hy => e.coe_fst hy, e.open_source, ex‚ü©
#align trivialization.coe_fst_eventually_eq_proj Trivialization.coe_fst_eventuallyEq_proj

theorem coe_fst_eventuallyEq_proj' (ex : proj x ‚àà e.baseSet) : Prod.fst ‚àò e =·∂†[ùìù x] proj :=
  e.coe_fst_eventuallyEq_proj (e.mem_source.2 ex)
#align trivialization.coe_fst_eventually_eq_proj' Trivialization.coe_fst_eventuallyEq_proj'

theorem map_proj_nhds (ex : x ‚àà e.source) : map proj (ùìù x) = ùìù (proj x) := by
  rw [‚Üê e.coe_fst ex, ‚Üê map_congr (e.coe_fst_eventuallyEq_proj ex), ‚Üê map_map, ‚Üê e.coe_coe,
    e.map_nhds_eq ex, map_fst_nhds]
#align trivialization.map_proj_nhds Trivialization.map_proj_nhds

theorem preimage_subset_source {s : Set B} (hb : s ‚äÜ e.baseSet) : proj ‚Åª¬π' s ‚äÜ e.source :=
  fun _p hp => e.mem_source.mpr (hb hp)
#align trivialization.preimage_subset_source Trivialization.preimage_subset_source

theorem image_preimage_eq_prod_univ {s : Set B} (hb : s ‚äÜ e.baseSet) :
    e '' (proj ‚Åª¬π' s) = s √óÀ¢ univ :=
  Subset.antisymm
    (image_subset_iff.mpr fun p hp =>
      ‚ü®(e.proj_toFun p (e.preimage_subset_source hb hp)).symm ‚ñ∏ hp, trivial‚ü©)
    fun p hp =>
    let hp' : p ‚àà e.target := e.mem_target.mpr (hb hp.1)
    ‚ü®e.invFun p, mem_preimage.mpr ((e.proj_symm_apply hp').symm ‚ñ∏ hp.1), e.apply_symm_apply hp'‚ü©
#align trivialization.image_preimage_eq_prod_univ Trivialization.image_preimage_eq_prod_univ

theorem tendsto_nhds_iff {Œ± : Type*} {l : Filter Œ±} {f : Œ± ‚Üí Z} {z : Z} (hz : z ‚àà e.source) :
    Tendsto f l (ùìù z) ‚Üî
      Tendsto (proj ‚àò f) l (ùìù (proj z)) ‚àß Tendsto (fun x ‚Ü¶ (e (f x)).2) l (ùìù (e z).2) := by
  rw [e.nhds_eq_comap_inf_principal hz, tendsto_inf, tendsto_comap_iff, Prod.tendsto_iff, coe_coe,
    tendsto_principal, coe_fst _ hz]
  by_cases hl : ‚àÄ·∂† x in l, f x ‚àà e.source
  ¬∑ simp only [hl, and_true]
    refine (tendsto_congr' ?_).and Iff.rfl
    exact hl.mono fun x ‚Ü¶ e.coe_fst
  ¬∑ simp only [hl, and_false, false_iff, not_and]
    rw [e.source_eq] at hl hz
    exact fun h _ ‚Ü¶ hl <| h <| e.open_baseSet.mem_nhds hz

theorem nhds_eq_inf_comap {z : Z} (hz : z ‚àà e.source) :
    ùìù z = comap proj (ùìù (proj z)) ‚äì comap (Prod.snd ‚àò e) (ùìù (e z).2) := by
  refine eq_of_forall_le_iff fun l ‚Ü¶ ?_
  rw [le_inf_iff, ‚Üê tendsto_iff_comap, ‚Üê tendsto_iff_comap]
  exact e.tendsto_nhds_iff hz

/-- The preimage of a subset of the base set is homeomorphic to the product with the fiber. -/
def preimageHomeomorph {s : Set B} (hb : s ‚äÜ e.baseSet) : proj ‚Åª¬π' s ‚âÉ‚Çú s √ó F :=
  (e.toPartialHomeomorph.homeomorphOfImageSubsetSource (e.preimage_subset_source hb)
        (e.image_preimage_eq_prod_univ hb)).trans
    ((Homeomorph.Set.prod s univ).trans ((Homeomorph.refl s).prodCongr (Homeomorph.Set.univ F)))
#align trivialization.preimage_homeomorph Trivialization.preimageHomeomorph

@[simp]
theorem preimageHomeomorph_apply {s : Set B} (hb : s ‚äÜ e.baseSet) (p : proj ‚Åª¬π' s) :
    e.preimageHomeomorph hb p = (‚ü®proj p, p.2‚ü©, (e p).2) :=
  Prod.ext (Subtype.ext (e.proj_toFun p (e.mem_source.mpr (hb p.2)))) rfl
#align trivialization.preimage_homeomorph_apply Trivialization.preimageHomeomorph_apply

/-- Auxilliary definition to avoid looping in `dsimp`
with `Trivialization.preimageHomeomorph_symm_apply`. -/
protected def preimageHomeomorph_symm_apply.aux {s : Set B} (hb : s ‚äÜ e.baseSet) :=
  (e.preimageHomeomorph hb).symm

@[simp]
theorem preimageHomeomorph_symm_apply {s : Set B} (hb : s ‚äÜ e.baseSet) (p : s √ó F) :
    (e.preimageHomeomorph hb).symm p =
      ‚ü®e.symm (p.1, p.2), ((preimageHomeomorph_symm_apply.aux e hb) p).2‚ü© :=
  rfl
#align trivialization.preimage_homeomorph_symm_apply Trivialization.preimageHomeomorph_symm_apply

/-- The source is homeomorphic to the product of the base set with the fiber. -/
def sourceHomeomorphBaseSetProd : e.source ‚âÉ‚Çú e.baseSet √ó F :=
  (Homeomorph.setCongr e.source_eq).trans (e.preimageHomeomorph subset_rfl)
#align trivialization.source_homeomorph_base_set_prod Trivialization.sourceHomeomorphBaseSetProd

@[simp]
theorem sourceHomeomorphBaseSetProd_apply (p : e.source) :
    e.sourceHomeomorphBaseSetProd p = (‚ü®proj p, e.mem_source.mp p.2‚ü©, (e p).2) :=
  e.preimageHomeomorph_apply subset_rfl ‚ü®p, e.mem_source.mp p.2‚ü©
#align trivialization.source_homeomorph_base_set_prod_apply Trivialization.sourceHomeomorphBaseSetProd_apply

/-- Auxilliary definition to avoid looping in `dsimp`
with `Trivialization.sourceHomeomorphBaseSetProd_symm_apply`. -/
protected def sourceHomeomorphBaseSetProd_symm_apply.aux := e.sourceHomeomorphBaseSetProd.symm

@[simp]
theorem sourceHomeomorphBaseSetProd_symm_apply (p : e.baseSet √ó F) :
    e.sourceHomeomorphBaseSetProd.symm p =
      ‚ü®e.symm (p.1, p.2), (sourceHomeomorphBaseSetProd_symm_apply.aux e p).2‚ü© :=
  rfl
#align trivialization.source_homeomorph_base_set_prod_symm_apply Trivialization.sourceHomeomorphBaseSetProd_symm_apply

/-- Each fiber of a trivialization is homeomorphic to the specified fiber. -/
def preimageSingletonHomeomorph {b : B} (hb : b ‚àà e.baseSet) : proj ‚Åª¬π' {b} ‚âÉ‚Çú F :=
  .trans (e.preimageHomeomorph (Set.singleton_subset_iff.mpr hb)) <|
    .trans (.prodCongr (Homeomorph.homeomorphOfUnique ({b} : Set B) PUnit.{1}) (Homeomorph.refl F))
      (Homeomorph.punitProd F)
#align trivialization.preimage_singleton_homeomorph Trivialization.preimageSingletonHomeomorph

@[simp]
theorem preimageSingletonHomeomorph_apply {b : B} (hb : b ‚àà e.baseSet) (p : proj ‚Åª¬π' {b}) :
    e.preimageSingletonHomeomorph hb p = (e p).2 :=
  rfl
#align trivialization.preimage_singleton_homeomorph_apply Trivialization.preimageSingletonHomeomorph_apply

@[simp]
theorem preimageSingletonHomeomorph_symm_apply {b : B} (hb : b ‚àà e.baseSet) (p : F) :
    (e.preimageSingletonHomeomorph hb).symm p =
      ‚ü®e.symm (b, p), by rw [mem_preimage, e.proj_symm_apply' hb, mem_singleton_iff]‚ü© :=
  rfl
#align trivialization.preimage_singleton_homeomorph_symm_apply Trivialization.preimageSingletonHomeomorph_symm_apply

/-- In the domain of a bundle trivialization, the projection is continuous-/
theorem continuousAt_proj (ex : x ‚àà e.source) : ContinuousAt proj x :=
  (e.map_proj_nhds ex).le
#align trivialization.continuous_at_proj Trivialization.continuousAt_proj

/-- Composition of a `Trivialization` and a `Homeomorph`. -/
protected def compHomeomorph {Z' : Type*} [TopologicalSpace Z'] (h : Z' ‚âÉ‚Çú Z) :
    Trivialization F (proj ‚àò h) where
  toPartialHomeomorph := h.toPartialHomeomorph.trans e.toPartialHomeomorph
  baseSet := e.baseSet
  open_baseSet := e.open_baseSet
  source_eq := by simp [source_eq, preimage_preimage, (¬∑ ‚àò ¬∑)]
  target_eq := by simp [target_eq]
  proj_toFun p hp := by
    have hp : h p ‚àà e.source := by simpa using hp
    simp [hp]
#align trivialization.comp_homeomorph Trivialization.compHomeomorph

/-- Read off the continuity of a function `f : Z ‚Üí X` at `z : Z` by transferring via a
trivialization of `Z` containing `z`. -/
theorem continuousAt_of_comp_right {X : Type*} [TopologicalSpace X] {f : Z ‚Üí X} {z : Z}
    (e : Trivialization F proj) (he : proj z ‚àà e.baseSet)
    (hf : ContinuousAt (f ‚àò e.toPartialEquiv.symm) (e z)) : ContinuousAt f z := by
  have hez : z ‚àà e.toPartialEquiv.symm.target := by
    rw [PartialEquiv.symm_target, e.mem_source]
    exact he
  rwa [e.toPartialHomeomorph.symm.continuousAt_iff_continuousAt_comp_right hez,
    PartialHomeomorph.symm_symm]
#align trivialization.continuous_at_of_comp_right Trivialization.continuousAt_of_comp_right

/-- Read off the continuity of a function `f : X ‚Üí Z` at `x : X` by transferring via a
trivialization of `Z` containing `f x`. -/
theorem continuousAt_of_comp_left {X : Type*} [TopologicalSpace X] {f : X ‚Üí Z} {x : X}
    (e : Trivialization F proj) (hf_proj : ContinuousAt (proj ‚àò f) x) (he : proj (f x) ‚àà e.baseSet)
    (hf : ContinuousAt (e ‚àò f) x) : ContinuousAt f x := by
  rw [e.continuousAt_iff_continuousAt_comp_left]
  ¬∑ exact hf
  rw [e.source_eq, ‚Üê preimage_comp]
  exact hf_proj.preimage_mem_nhds (e.open_baseSet.mem_nhds he)
#align trivialization.continuous_at_of_comp_left Trivialization.continuousAt_of_comp_left

variable (e' : Trivialization F (œÄ F E)) {x' : TotalSpace F E} {b : B} {y : E b}

protected theorem continuousOn : ContinuousOn e' e'.source :=
  e'.continuousOn_toFun
#align trivialization.continuous_on Trivialization.continuousOn

theorem coe_mem_source : ‚Üëy ‚àà e'.source ‚Üî b ‚àà e'.baseSet :=
  e'.mem_source
#align trivialization.coe_mem_source Trivialization.coe_mem_source

@[deprecated PartialHomeomorph.open_target (since := "2023-03-10")]
theorem open_target' : IsOpen e'.target := e'.open_target
#align trivialization.open_target Trivialization.open_target'

@[simp, mfld_simps]
theorem coe_coe_fst (hb : b ‚àà e'.baseSet) : (e' y).1 = b :=
  e'.coe_fst (e'.mem_source.2 hb)
#align trivialization.coe_coe_fst Trivialization.coe_coe_fst

theorem mk_mem_target {y : F} : (b, y) ‚àà e'.target ‚Üî b ‚àà e'.baseSet :=
  e'.toPretrivialization.mem_target
#align trivialization.mk_mem_target Trivialization.mk_mem_target

theorem symm_apply_apply {x : TotalSpace F E} (hx : x ‚àà e'.source) :
    e'.toPartialHomeomorph.symm (e' x) = x :=
  e'.toPartialEquiv.left_inv hx
#align trivialization.symm_apply_apply Trivialization.symm_apply_apply

@[simp, mfld_simps]
theorem symm_coe_proj {x : B} {y : F} (e : Trivialization F (œÄ F E)) (h : x ‚àà e.baseSet) :
    (e.toPartialHomeomorph.symm (x, y)).1 = x :=
  e.proj_symm_apply' h
#align trivialization.symm_coe_proj Trivialization.symm_coe_proj

section Zero

variable [‚àÄ x, Zero (E x)]

/-- A fiberwise inverse to `e'`. The function `F ‚Üí E x` that induces a local inverse
`B √ó F ‚Üí TotalSpace F E` of `e'` on `e'.baseSet`. It is defined to be `0` outside `e'.baseSet`. -/
protected noncomputable def symm (e : Trivialization F (œÄ F E)) (b : B) (y : F) : E b :=
  e.toPretrivialization.symm b y
#align trivialization.symm Trivialization.symm

theorem symm_apply (e : Trivialization F (œÄ F E)) {b : B} (hb : b ‚àà e.baseSet) (y : F) :
    e.symm b y = cast (congr_arg E (e.symm_coe_proj hb)) (e.toPartialHomeomorph.symm (b, y)).2 :=
  dif_pos hb
#align trivialization.symm_apply Trivialization.symm_apply

theorem symm_apply_of_not_mem (e : Trivialization F (œÄ F E)) {b : B} (hb : b ‚àâ e.baseSet) (y : F) :
    e.symm b y = 0 :=
  dif_neg hb
#align trivialization.symm_apply_of_not_mem Trivialization.symm_apply_of_not_mem

theorem mk_symm (e : Trivialization F (œÄ F E)) {b : B} (hb : b ‚àà e.baseSet) (y : F) :
    TotalSpace.mk b (e.symm b y) = e.toPartialHomeomorph.symm (b, y) :=
  e.toPretrivialization.mk_symm hb y
#align trivialization.mk_symm Trivialization.mk_symm

theorem symm_proj_apply (e : Trivialization F (œÄ F E)) (z : TotalSpace F E)
    (hz : z.proj ‚àà e.baseSet) : e.symm z.proj (e z).2 = z.2 :=
  e.toPretrivialization.symm_proj_apply z hz
#align trivialization.symm_proj_apply Trivialization.symm_proj_apply

theorem symm_apply_apply_mk (e : Trivialization F (œÄ F E)) {b : B} (hb : b ‚àà e.baseSet) (y : E b) :
    e.symm b (e ‚ü®b, y‚ü©).2 = y :=
  e.symm_proj_apply ‚ü®b, y‚ü© hb
#align trivialization.symm_apply_apply_mk Trivialization.symm_apply_apply_mk

theorem apply_mk_symm (e : Trivialization F (œÄ F E)) {b : B} (hb : b ‚àà e.baseSet) (y : F) :
    e ‚ü®b, e.symm b y‚ü© = (b, y) :=
  e.toPretrivialization.apply_mk_symm hb y
#align trivialization.apply_mk_symm Trivialization.apply_mk_symm

theorem continuousOn_symm (e : Trivialization F (œÄ F E)) :
    ContinuousOn (fun z : B √ó F => TotalSpace.mk' F z.1 (e.symm z.1 z.2)) (e.baseSet √óÀ¢ univ) := by
  have : ‚àÄ z ‚àà e.baseSet √óÀ¢ (univ : Set F),
      TotalSpace.mk z.1 (e.symm z.1 z.2) = e.toPartialHomeomorph.symm z := by
    rintro x ‚ü®hx : x.1 ‚àà e.baseSet, _‚ü©
    rw [e.mk_symm hx]
  refine ContinuousOn.congr ?_ this
  rw [‚Üê e.target_eq]
  exact e.toPartialHomeomorph.continuousOn_symm
#align trivialization.continuous_on_symm Trivialization.continuousOn_symm

end Zero

/-- If `e` is a `Trivialization` of `proj : Z ‚Üí B` with fiber `F` and `h` is a homeomorphism
`F ‚âÉ‚Çú F'`, then `e.trans_fiber_homeomorph h` is the trivialization of `proj` with the fiber `F'`
that sends `p : Z` to `((e p).1, h (e p).2)`. -/
def transFiberHomeomorph {F' : Type*} [TopologicalSpace F'] (e : Trivialization F proj)
    (h : F ‚âÉ‚Çú F') : Trivialization F' proj where
  toPartialHomeomorph := e.toPartialHomeomorph.transHomeomorph <| (Homeomorph.refl _).prodCongr h
  baseSet := e.baseSet
  open_baseSet := e.open_baseSet
  source_eq := e.source_eq
  target_eq := by simp [target_eq, prod_univ, preimage_preimage]
  proj_toFun := e.proj_toFun
#align trivialization.trans_fiber_homeomorph Trivialization.transFiberHomeomorph

@[simp]
theorem transFiberHomeomorph_apply {F' : Type*} [TopologicalSpace F'] (e : Trivialization F proj)
    (h : F ‚âÉ‚Çú F') (x : Z) : e.transFiberHomeomorph h x = ((e x).1, h (e x).2) :=
  rfl
#align trivialization.trans_fiber_homeomorph_apply Trivialization.transFiberHomeomorph_apply

/-- Coordinate transformation in the fiber induced by a pair of bundle trivializations. See also
`Trivialization.coordChangeHomeomorph` for a version bundled as `F ‚âÉ‚Çú F`. -/
def coordChange (e‚ÇÅ e‚ÇÇ : Trivialization F proj) (b : B) (x : F) : F :=
  (e‚ÇÇ <| e‚ÇÅ.toPartialHomeomorph.symm (b, x)).2
#align trivialization.coord_change Trivialization.coordChange

theorem mk_coordChange (e‚ÇÅ e‚ÇÇ : Trivialization F proj) {b : B} (h‚ÇÅ : b ‚àà e‚ÇÅ.baseSet)
    (h‚ÇÇ : b ‚àà e‚ÇÇ.baseSet) (x : F) :
    (b, e‚ÇÅ.coordChange e‚ÇÇ b x) = e‚ÇÇ (e‚ÇÅ.toPartialHomeomorph.symm (b, x)) := by
  refine Prod.ext ?_ rfl
  rw [e‚ÇÇ.coe_fst', ‚Üê e‚ÇÅ.coe_fst', e‚ÇÅ.apply_symm_apply' h‚ÇÅ]
  ¬∑ rwa [e‚ÇÅ.proj_symm_apply' h‚ÇÅ]
  ¬∑ rwa [e‚ÇÅ.proj_symm_apply' h‚ÇÅ]
#align trivialization.mk_coord_change Trivialization.mk_coordChange

theorem coordChange_apply_snd (e‚ÇÅ e‚ÇÇ : Trivialization F proj) {p : Z} (h : proj p ‚àà e‚ÇÅ.baseSet) :
    e‚ÇÅ.coordChange e‚ÇÇ (proj p) (e‚ÇÅ p).snd = (e‚ÇÇ p).snd := by
  rw [coordChange, e‚ÇÅ.symm_apply_mk_proj (e‚ÇÅ.mem_source.2 h)]
#align trivialization.coord_change_apply_snd Trivialization.coordChange_apply_snd

theorem coordChange_same_apply (e : Trivialization F proj) {b : B} (h : b ‚àà e.baseSet) (x : F) :
    e.coordChange e b x = x := by rw [coordChange, e.apply_symm_apply' h]
#align trivialization.coord_change_same_apply Trivialization.coordChange_same_apply

theorem coordChange_same (e : Trivialization F proj) {b : B} (h : b ‚àà e.baseSet) :
    e.coordChange e b = id :=
  funext <| e.coordChange_same_apply h
#align trivialization.coord_change_same Trivialization.coordChange_same

theorem coordChange_coordChange (e‚ÇÅ e‚ÇÇ e‚ÇÉ : Trivialization F proj) {b : B} (h‚ÇÅ : b ‚àà e‚ÇÅ.baseSet)
    (h‚ÇÇ : b ‚àà e‚ÇÇ.baseSet) (x : F) :
    e‚ÇÇ.coordChange e‚ÇÉ b (e‚ÇÅ.coordChange e‚ÇÇ b x) = e‚ÇÅ.coordChange e‚ÇÉ b x := by
  rw [coordChange, e‚ÇÅ.mk_coordChange _ h‚ÇÅ h‚ÇÇ, ‚Üê e‚ÇÇ.coe_coe, e‚ÇÇ.left_inv, coordChange]
  rwa [e‚ÇÇ.mem_source, e‚ÇÅ.proj_symm_apply' h‚ÇÅ]
#align trivialization.coord_change_coord_change Trivialization.coordChange_coordChange

theorem continuous_coordChange (e‚ÇÅ e‚ÇÇ : Trivialization F proj) {b : B} (h‚ÇÅ : b ‚àà e‚ÇÅ.baseSet)
    (h‚ÇÇ : b ‚àà e‚ÇÇ.baseSet) : Continuous (e‚ÇÅ.coordChange e‚ÇÇ b) := by
  refine continuous_snd.comp (e‚ÇÇ.toPartialHomeomorph.continuousOn.comp_continuous
    (e‚ÇÅ.toPartialHomeomorph.continuousOn_symm.comp_continuous ?_ ?_) ?_)
  ¬∑ exact continuous_const.prod_mk continuous_id
  ¬∑ exact fun x => e‚ÇÅ.mem_target.2 h‚ÇÅ
  ¬∑ intro x
    rwa [e‚ÇÇ.mem_source, e‚ÇÅ.proj_symm_apply' h‚ÇÅ]
#align trivialization.continuous_coord_change Trivialization.continuous_coordChange

/-- Coordinate transformation in the fiber induced by a pair of bundle trivializations,
as a homeomorphism. -/
protected def coordChangeHomeomorph (e‚ÇÅ e‚ÇÇ : Trivialization F proj) {b : B} (h‚ÇÅ : b ‚àà e‚ÇÅ.baseSet)
    (h‚ÇÇ : b ‚àà e‚ÇÇ.baseSet) : F ‚âÉ‚Çú F where
  toFun := e‚ÇÅ.coordChange e‚ÇÇ b
  invFun := e‚ÇÇ.coordChange e‚ÇÅ b
  left_inv x := by simp only [*, coordChange_coordChange, coordChange_same_apply]
  right_inv x := by simp only [*, coordChange_coordChange, coordChange_same_apply]
  continuous_toFun := e‚ÇÅ.continuous_coordChange e‚ÇÇ h‚ÇÅ h‚ÇÇ
  continuous_invFun := e‚ÇÇ.continuous_coordChange e‚ÇÅ h‚ÇÇ h‚ÇÅ
#align trivialization.coord_change_homeomorph Trivialization.coordChangeHomeomorph

@[simp]
theorem coordChangeHomeomorph_coe (e‚ÇÅ e‚ÇÇ : Trivialization F proj) {b : B} (h‚ÇÅ : b ‚àà e‚ÇÅ.baseSet)
    (h‚ÇÇ : b ‚àà e‚ÇÇ.baseSet) : ‚áë(e‚ÇÅ.coordChangeHomeomorph e‚ÇÇ h‚ÇÅ h‚ÇÇ) = e‚ÇÅ.coordChange e‚ÇÇ b :=
  rfl
#align trivialization.coord_change_homeomorph_coe Trivialization.coordChangeHomeomorph_coe

variable {B' : Type*} [TopologicalSpace B']

theorem isImage_preimage_prod (e : Trivialization F proj) (s : Set B) :
    e.toPartialHomeomorph.IsImage (proj ‚Åª¬π' s) (s √óÀ¢ univ) := fun x hx => by simp [e.coe_fst', hx]
#align trivialization.is_image_preimage_prod Trivialization.isImage_preimage_prod

/-- Restrict a `Trivialization` to an open set in the base. -/
protected def restrOpen (e : Trivialization F proj) (s : Set B) (hs : IsOpen s) :
    Trivialization F proj where
  toPartialHomeomorph :=
    ((e.isImage_preimage_prod s).symm.restr (IsOpen.inter e.open_target (hs.prod isOpen_univ))).symm
  baseSet := e.baseSet ‚à© s
  open_baseSet := IsOpen.inter e.open_baseSet hs
  source_eq := by simp [source_eq]
  target_eq := by simp [target_eq, prod_univ]
  proj_toFun p hp := e.proj_toFun p hp.1
#align trivialization.restr_open Trivialization.restrOpen

section Piecewise

theorem frontier_preimage (e : Trivialization F proj) (s : Set B) :
    e.source ‚à© frontier (proj ‚Åª¬π' s) = proj ‚Åª¬π' (e.baseSet ‚à© frontier s) := by
  rw [‚Üê (e.isImage_preimage_prod s).frontier.preimage_eq, frontier_prod_univ_eq,
    (e.isImage_preimage_prod _).preimage_eq, e.source_eq, preimage_inter]
#align trivialization.frontier_preimage Trivialization.frontier_preimage

/-- Given two bundle trivializations `e`, `e'` of `proj : Z ‚Üí B` and a set `s : Set B` such that
the base sets of `e` and `e'` intersect `frontier s` on the same set and `e p = e' p` whenever
`proj p ‚àà e.baseSet ‚à© frontier s`, `e.piecewise e' s Hs Heq` is the bundle trivialization over
`Set.ite s e.baseSet e'.baseSet` that is equal to `e` on `proj ‚Åª¬π s` and is equal to `e'`
otherwise. -/
noncomputable def piecewise (e e' : Trivialization F proj) (s : Set B)
    (Hs : e.baseSet ‚à© frontier s = e'.baseSet ‚à© frontier s)
    (Heq : EqOn e e' <| proj ‚Åª¬π' (e.baseSet ‚à© frontier s)) : Trivialization F proj where
  toPartialHomeomorph :=
    e.toPartialHomeomorph.piecewise e'.toPartialHomeomorph (proj ‚Åª¬π' s) (s √óÀ¢ univ)
      (e.isImage_preimage_prod s) (e'.isImage_preimage_prod s)
      (by rw [e.frontier_preimage, e'.frontier_preimage, Hs]) (by rwa [e.frontier_preimage])
  baseSet := s.ite e.baseSet e'.baseSet
  open_baseSet := e.open_baseSet.ite e'.open_baseSet Hs
  source_eq := by simp [source_eq]
  target_eq := by simp [target_eq, prod_univ]
  proj_toFun p := by
    rintro (‚ü®he, hs‚ü© | ‚ü®he, hs‚ü©) <;> simp [*]
#align trivialization.piecewise Trivialization.piecewise

/-- Given two bundle trivializations `e`, `e'` of a topological fiber bundle `proj : Z ‚Üí B`
over a linearly ordered base `B` and a point `a ‚àà e.baseSet ‚à© e'.baseSet` such that
`e` equals `e'` on `proj ‚Åª¬π' {a}`, `e.piecewise_le_of_eq e' a He He' Heq` is the bundle
trivialization over `Set.ite (Iic a) e.baseSet e'.baseSet` that is equal to `e` on points `p`
such that `proj p ‚â§ a` and is equal to `e'` otherwise. -/
noncomputable def piecewiseLeOfEq [LinearOrder B] [OrderTopology B] (e e' : Trivialization F proj)
    (a : B) (He : a ‚àà e.baseSet) (He' : a ‚àà e'.baseSet) (Heq : ‚àÄ p, proj p = a ‚Üí e p = e' p) :
    Trivialization F proj :=
  e.piecewise e' (Iic a)
    (Set.ext fun x => and_congr_left_iff.2 fun hx => by
      obtain rfl : x = a := mem_singleton_iff.1 (frontier_Iic_subset _ hx)
      simp [He, He'])
    fun p hp => Heq p <| frontier_Iic_subset _ hp.2
#align trivialization.piecewise_le_of_eq Trivialization.piecewiseLeOfEq

/-- Given two bundle trivializations `e`, `e'` of a topological fiber bundle `proj : Z ‚Üí B` over a
linearly ordered base `B` and a point `a ‚àà e.baseSet ‚à© e'.baseSet`, `e.piecewise_le e' a He He'`
is the bundle trivialization over `Set.ite (Iic a) e.baseSet e'.baseSet` that is equal to `e` on
points `p` such that `proj p ‚â§ a` and is equal to `((e' p).1, h (e' p).2)` otherwise, where
`h = e'.coord_change_homeomorph e _ _` is the homeomorphism of the fiber such that
`h (e' p).2 = (e p).2` whenever `e p = a`. -/
noncomputable def piecewiseLe [LinearOrder B] [OrderTopology B] (e e' : Trivialization F proj)
    (a : B) (He : a ‚àà e.baseSet) (He' : a ‚àà e'.baseSet) : Trivialization F proj :=
  e.piecewiseLeOfEq (e'.transFiberHomeomorph (e'.coordChangeHomeomorph e He' He)) a He He' <| by
    rintro p rfl
    ext1
    ¬∑ simp [e.coe_fst', e'.coe_fst', *]
    ¬∑ simp [coordChange_apply_snd, *]
#align trivialization.piecewise_le Trivialization.piecewiseLe

/-- Given two bundle trivializations `e`, `e'` over disjoint sets, `e.disjoint_union e' H` is the
bundle trivialization over the union of the base sets that agrees with `e` and `e'` over their
base sets. -/
noncomputable def disjointUnion (e e' : Trivialization F proj) (H : Disjoint e.baseSet e'.baseSet) :
    Trivialization F proj where
  toPartialHomeomorph :=
    e.toPartialHomeomorph.disjointUnion e'.toPartialHomeomorph
      (by
        rw [e.source_eq, e'.source_eq]
        exact H.preimage _)
      (by
        rw [e.target_eq, e'.target_eq, disjoint_iff_inf_le]
        intro x hx
        exact H.le_bot ‚ü®hx.1.1, hx.2.1‚ü©)
  baseSet := e.baseSet ‚à™ e'.baseSet
  open_baseSet := IsOpen.union e.open_baseSet e'.open_baseSet
  source_eq := congr_arg‚ÇÇ (¬∑ ‚à™ ¬∑) e.source_eq e'.source_eq
  target_eq := (congr_arg‚ÇÇ (¬∑ ‚à™ ¬∑) e.target_eq e'.target_eq).trans union_prod.symm
  proj_toFun := by
    rintro p (hp | hp')
    ¬∑ show (e.source.piecewise e e' p).1 = proj p
      rw [piecewise_eq_of_mem, e.coe_fst] <;> exact hp
    ¬∑ show (e.source.piecewise e e' p).1 = proj p
      rw [piecewise_eq_of_not_mem, e'.coe_fst hp']
      simp only [source_eq] at hp' ‚ä¢
      exact fun h => H.le_bot ‚ü®h, hp'‚ü©
#align trivialization.disjoint_union Trivialization.disjointUnion

end Piecewise

end Trivialization
