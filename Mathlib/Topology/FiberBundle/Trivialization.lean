/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Data.Bundle
import Mathlib.Data.Set.Image
import Mathlib.Topology.CompactOpen
import Mathlib.Topology.PartialHomeomorph
import Mathlib.Topology.Order.Basic

/-!
# Trivializations

## Main definitions

### Basic definitions

* `Trivialization F p` : structure extending partial homeomorphisms, defining a local
  trivialization of a topological space `Z` with projection `p` and fiber `F`.

* `Pretrivialization F proj` : trivialization as a partial equivalence, mainly used when the
  topology on the total space has not yet been defined.

### Operations on bundles

We provide the following operations on `Trivialization`s.

* `Trivialization.compHomeomorph`: given a local trivialization `e` of a fiber bundle
  `p : Z ‚Üí B` and a homeomorphism `h : Z' ‚âÉ‚Çú Z`, returns a local trivialization of the fiber bundle
  `p ‚àò h`.

## Implementation notes

Previously, in mathlib, there was a structure `topological_vector_bundle.trivialization` which
extended another structure `topological_fiber_bundle.trivialization` by a linearity hypothesis. As
of PR https://github.com/leanprover-community/mathlib3/pull/17359, we have changed this to a single structure
`Trivialization` (no namespace), together with a mixin class `Trivialization.IsLinear`.

This permits all the *data* of a vector bundle to be held at the level of fiber bundles, so that the
same trivializations can underlie an object's structure as (say) a vector bundle over `‚ÑÇ` and as a
vector bundle over `‚Ñù`, as well as its structure simply as a fiber bundle.

This might be a little surprising, given the general trend of the library to ever-increased
bundling.  But in this case the typical motivation for more bundling does not apply: there is no
algebraic or order structure on the whole type of linear (say) trivializations of a bundle.
Indeed, since trivializations only have meaning on their base sets (taking junk values outside), the
type of linear trivializations is not even particularly well-behaved.
-/

open TopologicalSpace Filter Set Bundle Function
open scoped Topology

variable {B : Type*} (F : Type*) {E : B ‚Üí Type*}
variable {Z : Type*} [TopologicalSpace B] [TopologicalSpace F] {proj : Z ‚Üí B}

/-- This structure contains the information left for a local trivialization (which is implemented
below as `Trivialization F proj`) if the total space has not been given a topology, but we
have a topology on both the fiber and the base space. Through the construction
`topological_fiber_prebundle F proj` it will be possible to promote a
`Pretrivialization F proj` to a `Trivialization F proj`. -/
structure Pretrivialization (proj : Z ‚Üí B) extends PartialEquiv Z (B √ó F) where
  open_target : IsOpen target
  baseSet : Set B
  open_baseSet : IsOpen baseSet
  source_eq : source = proj ‚Åª¬π' baseSet
  target_eq : target = baseSet √óÀ¢ univ
  proj_toFun : ‚àÄ p ‚àà source, (toFun p).1 = proj p

namespace Pretrivialization

variable {F}
variable (e : Pretrivialization F proj) {x : Z}

/-- Coercion of a pretrivialization to a function. We don't use `e.toFun` in the `CoeFun` instance
because it is actually `e.toPartialEquiv.toFun`, so `simp` will apply lemmas about
`toPartialEquiv`. While we may want to switch to this behavior later, doing it mid-port will break a
lot of proofs. -/
@[coe] def toFun' : Z ‚Üí (B √ó F) := e.toFun

instance : CoeFun (Pretrivialization F proj) fun _ => Z ‚Üí B √ó F := ‚ü®toFun'‚ü©

@[ext]
lemma ext' (e e' : Pretrivialization F proj) (h‚ÇÅ : e.toPartialEquiv = e'.toPartialEquiv)
    (h‚ÇÇ : e.baseSet = e'.baseSet) : e = e' := by
  cases e; cases e'; congr

-- TODO: move `ext` here?
lemma ext {e e' : Pretrivialization F proj} (h‚ÇÅ : ‚àÄ x, e x = e' x)
    (h‚ÇÇ : ‚àÄ x, e.toPartialEquiv.symm x = e'.toPartialEquiv.symm x) (h‚ÇÉ : e.baseSet = e'.baseSet) :
    e = e' := by
  ext1 <;> [ext1; exact h‚ÇÉ]
  ¬∑ apply h‚ÇÅ
  ¬∑ apply h‚ÇÇ
  ¬∑ rw [e.source_eq, e'.source_eq, h‚ÇÉ]

/-- If the fiber is nonempty, then the projection also is. -/
lemma toPartialEquiv_injective [Nonempty F] :
    Injective (toPartialEquiv : Pretrivialization F proj ‚Üí PartialEquiv Z (B √ó F)) := by
  refine fun e e' h ‚Ü¶ ext' _ _ h ?_
  simpa only [fst_image_prod, univ_nonempty, target_eq]
    using congr_arg (Prod.fst '' PartialEquiv.target ¬∑) h

@[simp, mfld_simps]
theorem coe_coe : ‚áëe.toPartialEquiv = e :=
  rfl

@[simp, mfld_simps]
theorem coe_fst (ex : x ‚àà e.source) : (e x).1 = proj x :=
  e.proj_toFun x ex

theorem mem_source : x ‚àà e.source ‚Üî proj x ‚àà e.baseSet := by rw [e.source_eq, mem_preimage]

theorem coe_fst' (ex : proj x ‚àà e.baseSet) : (e x).1 = proj x :=
  e.coe_fst (e.mem_source.2 ex)

protected theorem eqOn : EqOn (Prod.fst ‚àò e) proj e.source := fun _ hx => e.coe_fst hx

theorem mk_proj_snd (ex : x ‚àà e.source) : (proj x, (e x).2) = e x :=
  Prod.ext (e.coe_fst ex).symm rfl

theorem mk_proj_snd' (ex : proj x ‚àà e.baseSet) : (proj x, (e x).2) = e x :=
  Prod.ext (e.coe_fst' ex).symm rfl

/-- Composition of inverse and coercion from the subtype of the target. -/
def setSymm : e.target ‚Üí Z :=
  e.target.restrict e.toPartialEquiv.symm

theorem mem_target {x : B √ó F} : x ‚àà e.target ‚Üî x.1 ‚àà e.baseSet := by
  rw [e.target_eq, prod_univ, mem_preimage]

theorem proj_symm_apply {x : B √ó F} (hx : x ‚àà e.target) : proj (e.toPartialEquiv.symm x) = x.1 := by
  have := (e.coe_fst (e.map_target hx)).symm
  rwa [‚Üê e.coe_coe, e.right_inv hx] at this

theorem proj_symm_apply' {b : B} {x : F} (hx : b ‚àà e.baseSet) :
    proj (e.toPartialEquiv.symm (b, x)) = b :=
  e.proj_symm_apply (e.mem_target.2 hx)

theorem proj_surjOn_baseSet [Nonempty F] : Set.SurjOn proj e.source e.baseSet := fun b hb =>
  let ‚ü®y‚ü© := ‚ÄπNonempty F‚Ä∫
  ‚ü®e.toPartialEquiv.symm (b, y), e.toPartialEquiv.map_target <| e.mem_target.2 hb,
    e.proj_symm_apply' hb‚ü©

theorem apply_symm_apply {x : B √ó F} (hx : x ‚àà e.target) : e (e.toPartialEquiv.symm x) = x :=
  e.toPartialEquiv.right_inv hx

theorem apply_symm_apply' {b : B} {x : F} (hx : b ‚àà e.baseSet) :
    e (e.toPartialEquiv.symm (b, x)) = (b, x) :=
  e.apply_symm_apply (e.mem_target.2 hx)

theorem symm_apply_apply {x : Z} (hx : x ‚àà e.source) : e.toPartialEquiv.symm (e x) = x :=
  e.toPartialEquiv.left_inv hx

@[simp, mfld_simps]
theorem symm_apply_mk_proj {x : Z} (ex : x ‚àà e.source) :
    e.toPartialEquiv.symm (proj x, (e x).2) = x := by
  rw [‚Üê e.coe_fst ex, ‚Üê e.coe_coe, e.left_inv ex]

@[simp, mfld_simps]
theorem preimage_symm_proj_baseSet :
    e.toPartialEquiv.symm ‚Åª¬π' (proj ‚Åª¬π' e.baseSet) ‚à© e.target = e.target := by
  refine inter_eq_right.mpr fun x hx => ?_
  simp only [mem_preimage, PartialEquiv.invFun_as_coe, e.proj_symm_apply hx]
  exact e.mem_target.mp hx

@[simp, mfld_simps]
theorem preimage_symm_proj_inter (s : Set B) :
    e.toPartialEquiv.symm ‚Åª¬π' (proj ‚Åª¬π' s) ‚à© e.baseSet √óÀ¢ univ = (s ‚à© e.baseSet) √óÀ¢ univ := by
  ext ‚ü®x, y‚ü©
  suffices x ‚àà e.baseSet ‚Üí (proj (e.toPartialEquiv.symm (x, y)) ‚àà s ‚Üî x ‚àà s) by
    simpa only [prodMk_mem_set_prod_eq, mem_inter_iff, and_true, mem_univ, and_congr_left_iff]
  intro h
  rw [e.proj_symm_apply' h]

theorem target_inter_preimage_symm_source_eq (e f : Pretrivialization F proj) :
    f.target ‚à© f.toPartialEquiv.symm ‚Åª¬π' e.source = (e.baseSet ‚à© f.baseSet) √óÀ¢ univ := by
  rw [inter_comm, f.target_eq, e.source_eq, f.preimage_symm_proj_inter]

theorem trans_source (e f : Pretrivialization F proj) :
    (f.toPartialEquiv.symm.trans e.toPartialEquiv).source = (e.baseSet ‚à© f.baseSet) √óÀ¢ univ := by
  rw [PartialEquiv.trans_source, PartialEquiv.symm_source, e.target_inter_preimage_symm_source_eq]

theorem symm_trans_symm (e e' : Pretrivialization F proj) :
    (e.toPartialEquiv.symm.trans e'.toPartialEquiv).symm
      = e'.toPartialEquiv.symm.trans e.toPartialEquiv := by
  rw [PartialEquiv.trans_symm_eq_symm_trans_symm, PartialEquiv.symm_symm]

theorem symm_trans_source_eq (e e' : Pretrivialization F proj) :
    (e.toPartialEquiv.symm.trans e'.toPartialEquiv).source = (e.baseSet ‚à© e'.baseSet) √óÀ¢ univ := by
  rw [PartialEquiv.trans_source, e'.source_eq, PartialEquiv.symm_source, e.target_eq, inter_comm,
    e.preimage_symm_proj_inter, inter_comm]

theorem symm_trans_target_eq (e e' : Pretrivialization F proj) :
    (e.toPartialEquiv.symm.trans e'.toPartialEquiv).target = (e.baseSet ‚à© e'.baseSet) √óÀ¢ univ := by
  rw [‚Üê PartialEquiv.symm_source, symm_trans_symm, symm_trans_source_eq, inter_comm]

variable (e' : Pretrivialization F (œÄ F E)) {b : B} {y : E b}

@[simp]
theorem coe_mem_source : ‚Üëy ‚àà e'.source ‚Üî b ‚àà e'.baseSet :=
  e'.mem_source

@[mfld_simps]
theorem coe_coe_fst (hb : b ‚àà e'.baseSet) : (e' y).1 = b := by
  simp [hb]

theorem mk_mem_target {x : B} {y : F} : (x, y) ‚àà e'.target ‚Üî x ‚àà e'.baseSet :=
  e'.mem_target

theorem symm_coe_proj {x : B} {y : F} (e' : Pretrivialization F (œÄ F E)) (h : x ‚àà e'.baseSet) :
    (e'.toPartialEquiv.symm (x, y)).1 = x :=
  e'.proj_symm_apply' h

section Zero

variable [‚àÄ x, Zero (E x)]

open Classical in
/-- A fiberwise inverse to `e`. This is the function `F ‚Üí E b` that induces a local inverse
`B √ó F ‚Üí TotalSpace F E` of `e` on `e.baseSet`. It is defined to be `0` outside `e.baseSet`. -/
protected noncomputable def symm (e : Pretrivialization F (œÄ F E)) (b : B) (y : F) : E b :=
  if hb : b ‚àà e.baseSet then
    cast (congr_arg E (e.proj_symm_apply' hb)) (e.toPartialEquiv.symm (b, y)).2
  else 0

theorem symm_apply (e : Pretrivialization F (œÄ F E)) {b : B} (hb : b ‚àà e.baseSet) (y : F) :
    e.symm b y = cast (congr_arg E (e.symm_coe_proj hb)) (e.toPartialEquiv.symm (b, y)).2 :=
  dif_pos hb

theorem symm_apply_of_notMem (e : Pretrivialization F (œÄ F E)) {b : B} (hb : b ‚àâ e.baseSet)
    (y : F) : e.symm b y = 0 :=
  dif_neg hb

@[deprecated (since := "2025-05-23")] alias symm_apply_of_not_mem := symm_apply_of_notMem

theorem coe_symm_of_notMem (e : Pretrivialization F (œÄ F E)) {b : B} (hb : b ‚àâ e.baseSet) :
    (e.symm b : F ‚Üí E b) = 0 :=
  funext fun _ => dif_neg hb

@[deprecated (since := "2025-05-23")] alias coe_symm_of_not_mem := coe_symm_of_notMem

theorem mk_symm (e : Pretrivialization F (œÄ F E)) {b : B} (hb : b ‚àà e.baseSet) (y : F) :
    TotalSpace.mk b (e.symm b y) = e.toPartialEquiv.symm (b, y) := by
  simp only [e.symm_apply hb, TotalSpace.mk_cast (e.proj_symm_apply' hb), TotalSpace.eta]

theorem symm_proj_apply (e : Pretrivialization F (œÄ F E)) (z : TotalSpace F E)
    (hz : z.proj ‚àà e.baseSet) : e.symm z.proj (e z).2 = z.2 := by
  rw [e.symm_apply hz, cast_eq_iff_heq, e.mk_proj_snd' hz, e.symm_apply_apply (e.mem_source.mpr hz)]

theorem symm_apply_apply_mk (e : Pretrivialization F (œÄ F E)) {b : B} (hb : b ‚àà e.baseSet)
    (y : E b) : e.symm b (e ‚ü®b, y‚ü©).2 = y :=
  e.symm_proj_apply ‚ü®b, y‚ü© hb

theorem apply_mk_symm (e : Pretrivialization F (œÄ F E)) {b : B} (hb : b ‚àà e.baseSet) (y : F) :
    e ‚ü®b, e.symm b y‚ü© = (b, y) := by
  rw [e.mk_symm hb, e.apply_symm_apply (e.mk_mem_target.mpr hb)]

end Zero

end Pretrivialization

variable [TopologicalSpace Z] [TopologicalSpace (TotalSpace F E)]

/-- A structure extending partial homeomorphisms, defining a local trivialization of a projection
`proj : Z ‚Üí B` with fiber `F`, as a partial homeomorphism between `Z` and `B √ó F` defined between
two sets of the form `proj ‚Åª¬π' baseSet` and `baseSet √ó F`, acting trivially on the first coordinate.
-/
structure Trivialization (proj : Z ‚Üí B) extends PartialHomeomorph Z (B √ó F) where
  baseSet : Set B
  open_baseSet : IsOpen baseSet
  source_eq : source = proj ‚Åª¬π' baseSet
  target_eq : target = baseSet √óÀ¢ univ
  proj_toFun : ‚àÄ p ‚àà source, (toPartialHomeomorph p).1 = proj p

namespace Trivialization

variable {F}
variable (e : Trivialization F proj) {x : Z}

@[ext]
lemma ext' (e e' : Trivialization F proj) (h‚ÇÅ : e.toPartialHomeomorph = e'.toPartialHomeomorph)
    (h‚ÇÇ : e.baseSet = e'.baseSet) : e = e' := by
  cases e; cases e'; congr

/-- Coercion of a trivialization to a function. We don't use `e.toFun` in the `CoeFun` instance
because it is actually `e.toPartialEquiv.toFun`, so `simp` will apply lemmas about
`toPartialEquiv`. While we may want to switch to this behavior later, doing it mid-port will break a
lot of proofs. -/
@[coe] def toFun' : Z ‚Üí (B √ó F) := e.toFun

/-- Natural identification as a `Pretrivialization`. -/
def toPretrivialization : Pretrivialization F proj :=
  { e with }

instance : CoeFun (Trivialization F proj) fun _ => Z ‚Üí B √ó F := ‚ü®toFun'‚ü©

instance : Coe (Trivialization F proj) (Pretrivialization F proj) :=
  ‚ü®toPretrivialization‚ü©

/-- See Note [custom simps projection] -/
def Simps.apply (proj : Z ‚Üí B) (e : Trivialization F proj) : Z ‚Üí B √ó F := e

/-- See Note [custom simps projection] -/
noncomputable def Simps.symm_apply (proj : Z ‚Üí B) (e : Trivialization F proj) : B √ó F ‚Üí Z :=
  e.toPartialHomeomorph.symm

initialize_simps_projections Trivialization (toFun ‚Üí apply, invFun ‚Üí symm_apply)

theorem toPretrivialization_injective :
    Function.Injective fun e : Trivialization F proj => e.toPretrivialization := fun e e' h => by
  ext1
  exacts [PartialHomeomorph.toPartialEquiv_injective (congr_arg Pretrivialization.toPartialEquiv h),
    congr_arg Pretrivialization.baseSet h]

@[simp, mfld_simps]
theorem coe_coe : ‚áëe.toPartialHomeomorph = e :=
  rfl

@[simp, mfld_simps]
theorem coe_fst (ex : x ‚àà e.source) : (e x).1 = proj x :=
  e.proj_toFun x ex

protected theorem eqOn : EqOn (Prod.fst ‚àò e) proj e.source := fun _x hx => e.coe_fst hx

theorem mem_source : x ‚àà e.source ‚Üî proj x ‚àà e.baseSet := by rw [e.source_eq, mem_preimage]

theorem coe_fst' (ex : proj x ‚àà e.baseSet) : (e x).1 = proj x :=
  e.coe_fst (e.mem_source.2 ex)

theorem mk_proj_snd (ex : x ‚àà e.source) : (proj x, (e x).2) = e x :=
  Prod.ext (e.coe_fst ex).symm rfl

theorem mk_proj_snd' (ex : proj x ‚àà e.baseSet) : (proj x, (e x).2) = e x :=
  Prod.ext (e.coe_fst' ex).symm rfl

theorem source_inter_preimage_target_inter (s : Set (B √ó F)) :
    e.source ‚à© e ‚Åª¬π' (e.target ‚à© s) = e.source ‚à© e ‚Åª¬π' s :=
  e.toPartialHomeomorph.source_inter_preimage_target_inter s

@[simp, mfld_simps]
theorem coe_mk (e : PartialHomeomorph Z (B √ó F)) (i j k l m) (x : Z) :
    (Trivialization.mk e i j k l m : Trivialization F proj) x = e x :=
  rfl

theorem mem_target {x : B √ó F} : x ‚àà e.target ‚Üî x.1 ‚àà e.baseSet :=
  e.toPretrivialization.mem_target

theorem map_target {x : B √ó F} (hx : x ‚àà e.target) : e.toPartialHomeomorph.symm x ‚àà e.source :=
  e.toPartialHomeomorph.map_target hx

theorem proj_symm_apply {x : B √ó F} (hx : x ‚àà e.target) :
    proj (e.toPartialHomeomorph.symm x) = x.1 :=
  e.toPretrivialization.proj_symm_apply hx

theorem proj_symm_apply' {b : B} {x : F} (hx : b ‚àà e.baseSet) :
    proj (e.toPartialHomeomorph.symm (b, x)) = b :=
  e.toPretrivialization.proj_symm_apply' hx

theorem proj_surjOn_baseSet [Nonempty F] : Set.SurjOn proj e.source e.baseSet :=
  e.toPretrivialization.proj_surjOn_baseSet

theorem apply_symm_apply {x : B √ó F} (hx : x ‚àà e.target) : e (e.toPartialHomeomorph.symm x) = x :=
  e.toPartialHomeomorph.right_inv hx

theorem apply_symm_apply' {b : B} {x : F} (hx : b ‚àà e.baseSet) :
    e (e.toPartialHomeomorph.symm (b, x)) = (b, x) :=
  e.toPretrivialization.apply_symm_apply' hx

@[simp, mfld_simps]
theorem symm_apply_mk_proj (ex : x ‚àà e.source) : e.toPartialHomeomorph.symm (proj x, (e x).2) = x :=
  e.toPretrivialization.symm_apply_mk_proj ex

theorem symm_trans_source_eq (e e' : Trivialization F proj) :
    (e.toPartialEquiv.symm.trans e'.toPartialEquiv).source = (e.baseSet ‚à© e'.baseSet) √óÀ¢ univ :=
  Pretrivialization.symm_trans_source_eq e.toPretrivialization e'

theorem symm_trans_target_eq (e e' : Trivialization F proj) :
    (e.toPartialEquiv.symm.trans e'.toPartialEquiv).target = (e.baseSet ‚à© e'.baseSet) √óÀ¢ univ :=
  Pretrivialization.symm_trans_target_eq e.toPretrivialization e'

theorem coe_fst_eventuallyEq_proj (ex : x ‚àà e.source) : Prod.fst ‚àò e =·∂†[ùìù x] proj :=
  mem_nhds_iff.2 ‚ü®e.source, fun _y hy => e.coe_fst hy, e.open_source, ex‚ü©

theorem coe_fst_eventuallyEq_proj' (ex : proj x ‚àà e.baseSet) : Prod.fst ‚àò e =·∂†[ùìù x] proj :=
  e.coe_fst_eventuallyEq_proj (e.mem_source.2 ex)

theorem map_proj_nhds (ex : x ‚àà e.source) : map proj (ùìù x) = ùìù (proj x) := by
  rw [‚Üê e.coe_fst ex, ‚Üê map_congr (e.coe_fst_eventuallyEq_proj ex), ‚Üê map_map, ‚Üê e.coe_coe,
    e.map_nhds_eq ex, map_fst_nhds]

theorem preimage_subset_source {s : Set B} (hb : s ‚äÜ e.baseSet) : proj ‚Åª¬π' s ‚äÜ e.source :=
  fun _p hp => e.mem_source.mpr (hb hp)

theorem image_preimage_eq_prod_univ {s : Set B} (hb : s ‚äÜ e.baseSet) :
    e '' (proj ‚Åª¬π' s) = s √óÀ¢ univ :=
  Subset.antisymm
    (image_subset_iff.mpr fun p hp =>
      ‚ü®(e.proj_toFun p (e.preimage_subset_source hb hp)).symm ‚ñ∏ hp, trivial‚ü©)
    fun p hp =>
    let hp' : p ‚àà e.target := e.mem_target.mpr (hb hp.1)
    ‚ü®e.invFun p, mem_preimage.mpr ((e.proj_symm_apply hp').symm ‚ñ∏ hp.1), e.apply_symm_apply hp'‚ü©

theorem tendsto_nhds_iff {Œ± : Type*} {l : Filter Œ±} {f : Œ± ‚Üí Z} {z : Z} (hz : z ‚àà e.source) :
    Tendsto f l (ùìù z) ‚Üî
      Tendsto (proj ‚àò f) l (ùìù (proj z)) ‚àß Tendsto (fun x ‚Ü¶ (e (f x)).2) l (ùìù (e z).2) := by
  rw [e.nhds_eq_comap_inf_principal hz, tendsto_inf, tendsto_comap_iff, Prod.tendsto_iff, coe_coe,
    tendsto_principal, coe_fst _ hz]
  by_cases hl : ‚àÄ·∂† x in l, f x ‚àà e.source
  ¬∑ simp only [hl, and_true]
    refine (tendsto_congr' ?_).and Iff.rfl
    exact hl.mono fun x ‚Ü¶ e.coe_fst
  ¬∑ simp only [hl, and_false, false_iff, not_and]
    rw [e.source_eq] at hl hz
    exact fun h _ ‚Ü¶ hl <| h <| e.open_baseSet.mem_nhds hz

theorem nhds_eq_inf_comap {z : Z} (hz : z ‚àà e.source) :
    ùìù z = comap proj (ùìù (proj z)) ‚äì comap (Prod.snd ‚àò e) (ùìù (e z).2) := by
  refine eq_of_forall_le_iff fun l ‚Ü¶ ?_
  rw [le_inf_iff, ‚Üê tendsto_iff_comap, ‚Üê tendsto_iff_comap]
  exact e.tendsto_nhds_iff hz

/-- The preimage of a subset of the base set is homeomorphic to the product with the fiber. -/
def preimageHomeomorph {s : Set B} (hb : s ‚äÜ e.baseSet) : proj ‚Åª¬π' s ‚âÉ‚Çú s √ó F :=
  (e.toPartialHomeomorph.homeomorphOfImageSubsetSource (e.preimage_subset_source hb)
        (e.image_preimage_eq_prod_univ hb)).trans
    ((Homeomorph.Set.prod s univ).trans ((Homeomorph.refl s).prodCongr (Homeomorph.Set.univ F)))

@[simp]
theorem preimageHomeomorph_apply {s : Set B} (hb : s ‚äÜ e.baseSet) (p : proj ‚Åª¬π' s) :
    e.preimageHomeomorph hb p = (‚ü®proj p, p.2‚ü©, (e p).2) :=
  Prod.ext (Subtype.ext (e.proj_toFun p (e.mem_source.mpr (hb p.2)))) rfl

/-- Auxiliary definition to avoid looping in `dsimp`
with `Trivialization.preimageHomeomorph_symm_apply`. -/
protected def preimageHomeomorph_symm_apply.aux {s : Set B} (hb : s ‚äÜ e.baseSet) :=
  (e.preimageHomeomorph hb).symm

@[simp]
theorem preimageHomeomorph_symm_apply {s : Set B} (hb : s ‚äÜ e.baseSet) (p : s √ó F) :
    (e.preimageHomeomorph hb).symm p =
      ‚ü®e.symm (p.1, p.2), ((preimageHomeomorph_symm_apply.aux e hb) p).2‚ü© :=
  rfl

/-- The source is homeomorphic to the product of the base set with the fiber. -/
def sourceHomeomorphBaseSetProd : e.source ‚âÉ‚Çú e.baseSet √ó F :=
  (Homeomorph.setCongr e.source_eq).trans (e.preimageHomeomorph subset_rfl)

@[simp]
theorem sourceHomeomorphBaseSetProd_apply (p : e.source) :
    e.sourceHomeomorphBaseSetProd p = (‚ü®proj p, e.mem_source.mp p.2‚ü©, (e p).2) :=
  e.preimageHomeomorph_apply subset_rfl ‚ü®p, e.mem_source.mp p.2‚ü©

/-- Auxiliary definition to avoid looping in `dsimp`
with `Trivialization.sourceHomeomorphBaseSetProd_symm_apply`. -/
protected def sourceHomeomorphBaseSetProd_symm_apply.aux := e.sourceHomeomorphBaseSetProd.symm

@[simp]
theorem sourceHomeomorphBaseSetProd_symm_apply (p : e.baseSet √ó F) :
    e.sourceHomeomorphBaseSetProd.symm p =
      ‚ü®e.symm (p.1, p.2), (sourceHomeomorphBaseSetProd_symm_apply.aux e p).2‚ü© :=
  rfl

/-- Each fiber of a trivialization is homeomorphic to the specified fiber. -/
def preimageSingletonHomeomorph {b : B} (hb : b ‚àà e.baseSet) : proj ‚Åª¬π' {b} ‚âÉ‚Çú F :=
  .trans (e.preimageHomeomorph (Set.singleton_subset_iff.mpr hb)) <|
    .trans (.prodCongr (Homeomorph.homeomorphOfUnique ({b} : Set B) PUnit.{1}) (Homeomorph.refl F))
      (Homeomorph.punitProd F)

@[simp]
theorem preimageSingletonHomeomorph_apply {b : B} (hb : b ‚àà e.baseSet) (p : proj ‚Åª¬π' {b}) :
    e.preimageSingletonHomeomorph hb p = (e p).2 :=
  rfl

@[simp]
theorem preimageSingletonHomeomorph_symm_apply {b : B} (hb : b ‚àà e.baseSet) (p : F) :
    (e.preimageSingletonHomeomorph hb).symm p =
      ‚ü®e.symm (b, p), by rw [mem_preimage, e.proj_symm_apply' hb, mem_singleton_iff]‚ü© :=
  rfl

/-- In the domain of a bundle trivialization, the projection is continuous -/
theorem continuousAt_proj (ex : x ‚àà e.source) : ContinuousAt proj x :=
  (e.map_proj_nhds ex).le

/-- Composition of a `Trivialization` and a `Homeomorph`. -/
protected def compHomeomorph {Z' : Type*} [TopologicalSpace Z'] (h : Z' ‚âÉ‚Çú Z) :
    Trivialization F (proj ‚àò h) where
  toPartialHomeomorph := h.toPartialHomeomorph.trans e.toPartialHomeomorph
  baseSet := e.baseSet
  open_baseSet := e.open_baseSet
  source_eq := by simp [source_eq, preimage_preimage, Function.comp_def]
  target_eq := by simp [target_eq]
  proj_toFun p hp := by
    have hp : h p ‚àà e.source := by simpa using hp
    simp [hp]

/-- Read off the continuity of a function `f : Z ‚Üí X` at `z : Z` by transferring via a
trivialization of `Z` containing `z`. -/
theorem continuousAt_of_comp_right {X : Type*} [TopologicalSpace X] {f : Z ‚Üí X} {z : Z}
    (e : Trivialization F proj) (he : proj z ‚àà e.baseSet)
    (hf : ContinuousAt (f ‚àò e.toPartialEquiv.symm) (e z)) : ContinuousAt f z := by
  have hez : z ‚àà e.toPartialEquiv.symm.target := by
    rw [PartialEquiv.symm_target, e.mem_source]
    exact he
  rwa [e.toPartialHomeomorph.symm.continuousAt_iff_continuousAt_comp_right hez,
    PartialHomeomorph.symm_symm]

/-- Read off the continuity of a function `f : X ‚Üí Z` at `x : X` by transferring via a
trivialization of `Z` containing `f x`. -/
theorem continuousAt_of_comp_left {X : Type*} [TopologicalSpace X] {f : X ‚Üí Z} {x : X}
    (e : Trivialization F proj) (hf_proj : ContinuousAt (proj ‚àò f) x) (he : proj (f x) ‚àà e.baseSet)
    (hf : ContinuousAt (e ‚àò f) x) : ContinuousAt f x := by
  rw [e.continuousAt_iff_continuousAt_comp_left]
  ¬∑ exact hf
  rw [e.source_eq, ‚Üê preimage_comp]
  exact hf_proj.preimage_mem_nhds (e.open_baseSet.mem_nhds he)

variable (e' : Trivialization F (œÄ F E)) {b : B} {y : E b}

protected theorem continuousOn : ContinuousOn e' e'.source :=
  e'.continuousOn_toFun

theorem coe_mem_source : ‚Üëy ‚àà e'.source ‚Üî b ‚àà e'.baseSet :=
  e'.mem_source

@[simp, mfld_simps]
theorem coe_coe_fst (hb : b ‚àà e'.baseSet) : (e' y).1 = b :=
  e'.coe_fst (e'.mem_source.2 hb)

theorem mk_mem_target {y : F} : (b, y) ‚àà e'.target ‚Üî b ‚àà e'.baseSet :=
  e'.toPretrivialization.mem_target

theorem symm_apply_apply {x : TotalSpace F E} (hx : x ‚àà e'.source) :
    e'.toPartialHomeomorph.symm (e' x) = x :=
  e'.toPartialEquiv.left_inv hx

@[simp, mfld_simps]
theorem symm_coe_proj {x : B} {y : F} (e : Trivialization F (œÄ F E)) (h : x ‚àà e.baseSet) :
    (e.toPartialHomeomorph.symm (x, y)).1 = x :=
  e.proj_symm_apply' h

section Zero

variable [‚àÄ x, Zero (E x)]

/-- A fiberwise inverse to `e'`. The function `F ‚Üí E x` that induces a local inverse
`B √ó F ‚Üí TotalSpace F E` of `e'` on `e'.baseSet`. It is defined to be `0` outside `e'.baseSet`. -/
protected noncomputable def symm (e : Trivialization F (œÄ F E)) (b : B) (y : F) : E b :=
  e.toPretrivialization.symm b y

theorem symm_apply (e : Trivialization F (œÄ F E)) {b : B} (hb : b ‚àà e.baseSet) (y : F) :
    e.symm b y = cast (congr_arg E (e.symm_coe_proj hb)) (e.toPartialHomeomorph.symm (b, y)).2 :=
  dif_pos hb

theorem symm_apply_of_notMem (e : Trivialization F (œÄ F E)) {b : B} (hb : b ‚àâ e.baseSet) (y : F) :
    e.symm b y = 0 :=
  dif_neg hb

@[deprecated (since := "2025-05-23")] alias symm_apply_of_not_mem := symm_apply_of_notMem

theorem mk_symm (e : Trivialization F (œÄ F E)) {b : B} (hb : b ‚àà e.baseSet) (y : F) :
    TotalSpace.mk b (e.symm b y) = e.toPartialHomeomorph.symm (b, y) :=
  e.toPretrivialization.mk_symm hb y

theorem symm_proj_apply (e : Trivialization F (œÄ F E)) (z : TotalSpace F E)
    (hz : z.proj ‚àà e.baseSet) : e.symm z.proj (e z).2 = z.2 :=
  e.toPretrivialization.symm_proj_apply z hz

theorem symm_apply_apply_mk (e : Trivialization F (œÄ F E)) {b : B} (hb : b ‚àà e.baseSet) (y : E b) :
    e.symm b (e ‚ü®b, y‚ü©).2 = y :=
  e.symm_proj_apply ‚ü®b, y‚ü© hb

theorem apply_mk_symm (e : Trivialization F (œÄ F E)) {b : B} (hb : b ‚àà e.baseSet) (y : F) :
    e ‚ü®b, e.symm b y‚ü© = (b, y) :=
  e.toPretrivialization.apply_mk_symm hb y

theorem continuousOn_symm (e : Trivialization F (œÄ F E)) :
    ContinuousOn (fun z : B √ó F => TotalSpace.mk' F z.1 (e.symm z.1 z.2)) (e.baseSet √óÀ¢ univ) := by
  have : ‚àÄ z ‚àà e.baseSet √óÀ¢ (univ : Set F),
      TotalSpace.mk z.1 (e.symm z.1 z.2) = e.toPartialHomeomorph.symm z := by
    rintro x ‚ü®hx : x.1 ‚àà e.baseSet, _‚ü©
    rw [e.mk_symm hx]
  refine ContinuousOn.congr ?_ this
  rw [‚Üê e.target_eq]
  exact e.toPartialHomeomorph.continuousOn_symm

end Zero

/-- If `e` is a `Trivialization` of `proj : Z ‚Üí B` with fiber `F` and `h` is a homeomorphism
`F ‚âÉ‚Çú F'`, then `e.trans_fiber_homeomorph h` is the trivialization of `proj` with the fiber `F'`
that sends `p : Z` to `((e p).1, h (e p).2)`. -/
def transFiberHomeomorph {F' : Type*} [TopologicalSpace F'] (e : Trivialization F proj)
    (h : F ‚âÉ‚Çú F') : Trivialization F' proj where
  toPartialHomeomorph := e.toPartialHomeomorph.transHomeomorph <| (Homeomorph.refl _).prodCongr h
  baseSet := e.baseSet
  open_baseSet := e.open_baseSet
  source_eq := e.source_eq
  target_eq := by simp [target_eq, prod_univ, preimage_preimage]
  proj_toFun := e.proj_toFun

@[simp]
theorem transFiberHomeomorph_apply {F' : Type*} [TopologicalSpace F'] (e : Trivialization F proj)
    (h : F ‚âÉ‚Çú F') (x : Z) : e.transFiberHomeomorph h x = ((e x).1, h (e x).2) :=
  rfl

/-- Coordinate transformation in the fiber induced by a pair of bundle trivializations. See also
`Trivialization.coordChangeHomeomorph` for a version bundled as `F ‚âÉ‚Çú F`. -/
def coordChange (e‚ÇÅ e‚ÇÇ : Trivialization F proj) (b : B) (x : F) : F :=
  (e‚ÇÇ <| e‚ÇÅ.toPartialHomeomorph.symm (b, x)).2

theorem mk_coordChange (e‚ÇÅ e‚ÇÇ : Trivialization F proj) {b : B} (h‚ÇÅ : b ‚àà e‚ÇÅ.baseSet)
    (h‚ÇÇ : b ‚àà e‚ÇÇ.baseSet) (x : F) :
    (b, e‚ÇÅ.coordChange e‚ÇÇ b x) = e‚ÇÇ (e‚ÇÅ.toPartialHomeomorph.symm (b, x)) := by
  refine Prod.ext ?_ rfl
  rw [e‚ÇÇ.coe_fst', ‚Üê e‚ÇÅ.coe_fst', e‚ÇÅ.apply_symm_apply' h‚ÇÅ]
  ¬∑ rwa [e‚ÇÅ.proj_symm_apply' h‚ÇÅ]
  ¬∑ rwa [e‚ÇÅ.proj_symm_apply' h‚ÇÅ]

theorem coordChange_apply_snd (e‚ÇÅ e‚ÇÇ : Trivialization F proj) {p : Z} (h : proj p ‚àà e‚ÇÅ.baseSet) :
    e‚ÇÅ.coordChange e‚ÇÇ (proj p) (e‚ÇÅ p).snd = (e‚ÇÇ p).snd := by
  rw [coordChange, e‚ÇÅ.symm_apply_mk_proj (e‚ÇÅ.mem_source.2 h)]

theorem coordChange_same_apply (e : Trivialization F proj) {b : B} (h : b ‚àà e.baseSet) (x : F) :
    e.coordChange e b x = x := by rw [coordChange, e.apply_symm_apply' h]

theorem coordChange_same (e : Trivialization F proj) {b : B} (h : b ‚àà e.baseSet) :
    e.coordChange e b = id :=
  funext <| e.coordChange_same_apply h

theorem coordChange_coordChange (e‚ÇÅ e‚ÇÇ e‚ÇÉ : Trivialization F proj) {b : B} (h‚ÇÅ : b ‚àà e‚ÇÅ.baseSet)
    (h‚ÇÇ : b ‚àà e‚ÇÇ.baseSet) (x : F) :
    e‚ÇÇ.coordChange e‚ÇÉ b (e‚ÇÅ.coordChange e‚ÇÇ b x) = e‚ÇÅ.coordChange e‚ÇÉ b x := by
  rw [coordChange, e‚ÇÅ.mk_coordChange _ h‚ÇÅ h‚ÇÇ, ‚Üê e‚ÇÇ.coe_coe, e‚ÇÇ.left_inv, coordChange]
  rwa [e‚ÇÇ.mem_source, e‚ÇÅ.proj_symm_apply' h‚ÇÅ]

theorem continuous_coordChange (e‚ÇÅ e‚ÇÇ : Trivialization F proj) {b : B} (h‚ÇÅ : b ‚àà e‚ÇÅ.baseSet)
    (h‚ÇÇ : b ‚àà e‚ÇÇ.baseSet) : Continuous (e‚ÇÅ.coordChange e‚ÇÇ b) := by
  refine continuous_snd.comp (e‚ÇÇ.toPartialHomeomorph.continuousOn.comp_continuous
    (e‚ÇÅ.toPartialHomeomorph.continuousOn_symm.comp_continuous ?_ ?_) ?_)
  ¬∑ fun_prop
  ¬∑ exact fun x => e‚ÇÅ.mem_target.2 h‚ÇÅ
  ¬∑ intro x
    rwa [e‚ÇÇ.mem_source, e‚ÇÅ.proj_symm_apply' h‚ÇÅ]

/-- Coordinate transformation in the fiber induced by a pair of bundle trivializations,
as a homeomorphism. -/
protected def coordChangeHomeomorph (e‚ÇÅ e‚ÇÇ : Trivialization F proj) {b : B} (h‚ÇÅ : b ‚àà e‚ÇÅ.baseSet)
    (h‚ÇÇ : b ‚àà e‚ÇÇ.baseSet) : F ‚âÉ‚Çú F where
  toFun := e‚ÇÅ.coordChange e‚ÇÇ b
  invFun := e‚ÇÇ.coordChange e‚ÇÅ b
  left_inv x := by simp only [*, coordChange_coordChange, coordChange_same_apply]
  right_inv x := by simp only [*, coordChange_coordChange, coordChange_same_apply]
  continuous_toFun := e‚ÇÅ.continuous_coordChange e‚ÇÇ h‚ÇÅ h‚ÇÇ
  continuous_invFun := e‚ÇÇ.continuous_coordChange e‚ÇÅ h‚ÇÇ h‚ÇÅ

@[simp]
theorem coordChangeHomeomorph_coe (e‚ÇÅ e‚ÇÇ : Trivialization F proj) {b : B} (h‚ÇÅ : b ‚àà e‚ÇÅ.baseSet)
    (h‚ÇÇ : b ‚àà e‚ÇÇ.baseSet) : ‚áë(e‚ÇÅ.coordChangeHomeomorph e‚ÇÇ h‚ÇÅ h‚ÇÇ) = e‚ÇÅ.coordChange e‚ÇÇ b :=
  rfl

theorem isImage_preimage_prod (e : Trivialization F proj) (s : Set B) :
    e.toPartialHomeomorph.IsImage (proj ‚Åª¬π' s) (s √óÀ¢ univ) := fun x hx => by simp [e.coe_fst', hx]

/-- Restrict a `Trivialization` to an open set in the base. -/
protected def restrOpen (e : Trivialization F proj) (s : Set B) (hs : IsOpen s) :
    Trivialization F proj where
  toPartialHomeomorph :=
    ((e.isImage_preimage_prod s).symm.restr (IsOpen.inter e.open_target (hs.prod isOpen_univ))).symm
  baseSet := e.baseSet ‚à© s
  open_baseSet := IsOpen.inter e.open_baseSet hs
  source_eq := by simp [source_eq]
  target_eq := by simp [target_eq, prod_univ]
  proj_toFun p hp := e.proj_toFun p hp.1

section Piecewise

theorem frontier_preimage (e : Trivialization F proj) (s : Set B) :
    e.source ‚à© frontier (proj ‚Åª¬π' s) = proj ‚Åª¬π' (e.baseSet ‚à© frontier s) := by
  rw [‚Üê (e.isImage_preimage_prod s).frontier.preimage_eq, frontier_prod_univ_eq,
    (e.isImage_preimage_prod _).preimage_eq, e.source_eq, preimage_inter]

open Classical in
/-- Given two bundle trivializations `e`, `e'` of `proj : Z ‚Üí B` and a set `s : Set B` such that
the base sets of `e` and `e'` intersect `frontier s` on the same set and `e p = e' p` whenever
`proj p ‚àà e.baseSet ‚à© frontier s`, `e.piecewise e' s Hs Heq` is the bundle trivialization over
`Set.ite s e.baseSet e'.baseSet` that is equal to `e` on `proj ‚Åª¬π s` and is equal to `e'`
otherwise. -/
noncomputable def piecewise (e e' : Trivialization F proj) (s : Set B)
    (Hs : e.baseSet ‚à© frontier s = e'.baseSet ‚à© frontier s)
    (Heq : EqOn e e' <| proj ‚Åª¬π' (e.baseSet ‚à© frontier s)) : Trivialization F proj where
  toPartialHomeomorph :=
    e.toPartialHomeomorph.piecewise e'.toPartialHomeomorph (proj ‚Åª¬π' s) (s √óÀ¢ univ)
      (e.isImage_preimage_prod s) (e'.isImage_preimage_prod s)
      (by rw [e.frontier_preimage, e'.frontier_preimage, Hs]) (by rwa [e.frontier_preimage])
  baseSet := s.ite e.baseSet e'.baseSet
  open_baseSet := e.open_baseSet.ite e'.open_baseSet Hs
  source_eq := by simp [source_eq]
  target_eq := by simp [target_eq, prod_univ]
  proj_toFun p := by
    rintro (‚ü®he, hs‚ü© | ‚ü®he, hs‚ü©) <;> simp [*]

/-- Given two bundle trivializations `e`, `e'` of a topological fiber bundle `proj : Z ‚Üí B`
over a linearly ordered base `B` and a point `a ‚àà e.baseSet ‚à© e'.baseSet` such that
`e` equals `e'` on `proj ‚Åª¬π' {a}`, `e.piecewise_le_of_eq e' a He He' Heq` is the bundle
trivialization over `Set.ite (Iic a) e.baseSet e'.baseSet` that is equal to `e` on points `p`
such that `proj p ‚â§ a` and is equal to `e'` otherwise. -/
noncomputable def piecewiseLeOfEq [LinearOrder B] [OrderTopology B] (e e' : Trivialization F proj)
    (a : B) (He : a ‚àà e.baseSet) (He' : a ‚àà e'.baseSet) (Heq : ‚àÄ p, proj p = a ‚Üí e p = e' p) :
    Trivialization F proj :=
  e.piecewise e' (Iic a)
    (Set.ext fun x => and_congr_left_iff.2 fun hx => by
      obtain rfl : x = a := mem_singleton_iff.1 (frontier_Iic_subset _ hx)
      simp [He, He'])
    fun p hp => Heq p <| frontier_Iic_subset _ hp.2

/-- Given two bundle trivializations `e`, `e'` of a topological fiber bundle `proj : Z ‚Üí B` over a
linearly ordered base `B` and a point `a ‚àà e.baseSet ‚à© e'.baseSet`, `e.piecewise_le e' a He He'`
is the bundle trivialization over `Set.ite (Iic a) e.baseSet e'.baseSet` that is equal to `e` on
points `p` such that `proj p ‚â§ a` and is equal to `((e' p).1, h (e' p).2)` otherwise, where
`h = e'.coord_change_homeomorph e _ _` is the homeomorphism of the fiber such that
`h (e' p).2 = (e p).2` whenever `e p = a`. -/
noncomputable def piecewiseLe [LinearOrder B] [OrderTopology B] (e e' : Trivialization F proj)
    (a : B) (He : a ‚àà e.baseSet) (He' : a ‚àà e'.baseSet) : Trivialization F proj :=
  e.piecewiseLeOfEq (e'.transFiberHomeomorph (e'.coordChangeHomeomorph e He' He)) a He He' <| by
    rintro p rfl
    ext1
    ¬∑ simp [e.coe_fst', e'.coe_fst', *]
    ¬∑ simp [coordChange_apply_snd, *]

open Classical in
/-- Given two bundle trivializations `e`, `e'` over disjoint sets, `e.disjoint_union e' H` is the
bundle trivialization over the union of the base sets that agrees with `e` and `e'` over their
base sets. -/
noncomputable def disjointUnion (e e' : Trivialization F proj) (H : Disjoint e.baseSet e'.baseSet) :
    Trivialization F proj where
  toPartialHomeomorph :=
    e.toPartialHomeomorph.disjointUnion e'.toPartialHomeomorph
      (by
        rw [e.source_eq, e'.source_eq]
        exact H.preimage _)
      (by
        rw [e.target_eq, e'.target_eq, disjoint_iff_inf_le]
        intro x hx
        exact H.le_bot ‚ü®hx.1.1, hx.2.1‚ü©)
  baseSet := e.baseSet ‚à™ e'.baseSet
  open_baseSet := IsOpen.union e.open_baseSet e'.open_baseSet
  source_eq := congr_arg‚ÇÇ (¬∑ ‚à™ ¬∑) e.source_eq e'.source_eq
  target_eq := (congr_arg‚ÇÇ (¬∑ ‚à™ ¬∑) e.target_eq e'.target_eq).trans union_prod.symm
  proj_toFun := by
    rintro p (hp | hp')
    ¬∑ show (e.source.piecewise e e' p).1 = proj p
      rw [piecewise_eq_of_mem, e.coe_fst] <;> exact hp
    ¬∑ show (e.source.piecewise e e' p).1 = proj p
      rw [piecewise_eq_of_notMem, e'.coe_fst hp']
      simp only [source_eq] at hp' ‚ä¢
      exact fun h => H.le_bot ‚ü®h, hp'‚ü©

end Piecewise

section Lift

/-- The local lifting through a Trivialization `T` from the base to the leaf containing `z`. -/
def lift (T : Trivialization F proj) (z : Z) (b : B) : Z := T.invFun (b, (T z).2)

variable {T : Trivialization F proj} {z : Z} {b : B}

@[simp]
theorem lift_self (he : proj z ‚àà T.baseSet) : T.lift z (proj z) = z :=
  symm_apply_mk_proj _ <| T.mem_source.2 he

theorem proj_lift (hx : b ‚àà T.baseSet) : proj (T.lift z b) = b :=
  T.proj_symm_apply <| T.mem_target.2 hx

/-- The restriction of `lift` to the source and base set of `T`, as a bundled continuous map. -/
def liftCM (T : Trivialization F proj) : C(T.source √ó T.baseSet, T.source) where
  toFun ex := ‚ü®T.lift ex.1 ex.2, T.map_target (by simp [mem_target])‚ü©
  continuous_toFun := by
    apply Continuous.subtype_mk
    refine T.continuousOn_invFun.comp_continuous ?_ (by simp [mem_target])
    refine .prodMk (by fun_prop) (.snd ?_)
    exact T.continuousOn_toFun.comp_continuous (by fun_prop) (by simp)

variable {Œπ : Type*} [TopologicalSpace Œπ] [LocallyCompactPair Œπ T.baseSet]
  {Œ≥ : C(Œπ, T.baseSet)} {i : Œπ} {e : T.source}

/-- Extension of `liftCM` to continuous maps taking values in `T.baseSet` (local version of
homotopy lifting) -/
def clift (T : Trivialization F proj) [LocallyCompactPair Œπ T.baseSet] :
    C(T.source √ó C(Œπ, T.baseSet), C(Œπ, T.source)) := by
  let Œ® : C((T.source √ó C(Œπ, T.baseSet)) √ó Œπ, C(Œπ, T.baseSet) √ó Œπ) :=
    ‚ü®fun eŒ≥t => (eŒ≥t.1.2, eŒ≥t.2), by fun_prop‚ü©
  refine ContinuousMap.curry <| T.liftCM.comp <| ‚ü®fun eŒ≥t => ‚ü®eŒ≥t.1.1, eŒ≥t.1.2 eŒ≥t.2‚ü©, ?_‚ü©
  simpa using ‚ü®by fun_prop, ContinuousEval.continuous_eval.comp Œ®.continuous‚ü©

@[simp]
theorem clift_self (h : proj e.1 = Œ≥ i) :
    T.clift (e, Œ≥) i = e := by
  have : proj e ‚àà T.baseSet := by simp [h]
  simp [clift, liftCM, ‚Üê h, lift_self, this]

theorem proj_clift : proj (T.clift (e, Œ≥) i) = Œ≥ i := by
  simp [clift, liftCM, proj_lift]

end Lift

end Trivialization
