/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Floris van Doorn, Heather Macbeth
-/
import Mathlib.Topology.FiberBundle.Trivialization
import Mathlib.Topology.Order.LeftRightNhds

#align_import topology.fiber_bundle.basic from "leanprover-community/mathlib"@"e473c3198bb41f68560cab68a0529c854b618833"

/-!
# Fiber bundles

Mathematically, a (topological) fiber bundle with fiber `F` over a base `B` is a space projecting on
`B` for which the fibers are all homeomorphic to `F`, such that the local situation around each
point is a direct product.

In our formalism, a fiber bundle is by definition the type `Bundle.TotalSpace F E` where
`E : B ‚Üí Type*` is a function associating to `x : B` the fiber over `x`. This type
`Bundle.TotalSpace F E` is a type of pairs `‚ü®proj : B, snd : E proj‚ü©`.

To have a fiber bundle structure on `Bundle.TotalSpace F E`, one should
additionally have the following data:

* `F` should be a topological space;
* There should be a topology on `Bundle.TotalSpace F E`, for which the projection to `B` is
a fiber bundle with fiber `F` (in particular, each fiber `E x` is homeomorphic to `F`);
* For each `x`, the fiber `E x` should be a topological space, and the injection
from `E x` to `Bundle.TotalSpace F E` should be an embedding;
* There should be a distinguished set of bundle trivializations, the "trivialization atlas"
* There should be a choice of bundle trivialization at each point, which belongs to this atlas.

If all these conditions are satisfied, we register the typeclass `FiberBundle F E`.

It is in general nontrivial to construct a fiber bundle. A way is to start from the knowledge of
how changes of local trivializations act on the fiber. From this, one can construct the total space
of the bundle and its topology by a suitable gluing construction. The main content of this file is
an implementation of this construction: starting from an object of type
`FiberBundleCore` registering the trivialization changes, one gets the corresponding
fiber bundle and projection.

Similarly we implement the object `FiberPrebundle` which allows to define a topological
fiber bundle from trivializations given as partial equivalences with minimum additional properties.

## Main definitions

### Basic definitions

* `FiberBundle F E` : Structure saying that `E : B ‚Üí Type*` is a fiber bundle with fiber `F`.

### Construction of a bundle from trivializations

* `Bundle.TotalSpace F E` is the type of pairs `(proj : B, snd : E proj)`. We can use the extra
  argument `F` to construct topology on the total space.
* `FiberBundleCore Œπ B F` : structure registering how changes of coordinates act
  on the fiber `F` above open subsets of `B`, where local trivializations are indexed by `Œπ`.

Let `Z : FiberBundleCore Œπ B F`. Then we define

* `Z.Fiber x`     : the fiber above `x`, homeomorphic to `F` (and defeq to `F` as a type).
* `Z.TotalSpace`  : the total space of `Z`, defined as `Bundle.TotalSpace F Z.Fiber` with a custom
                    topology.
* `Z.proj`        : projection from `Z.TotalSpace` to `B`. It is continuous.
* `Z.localTriv i` : for `i : Œπ`, bundle trivialization above the set `Z.baseSet i`, which is an
                    open set in `B`.

* `FiberPrebundle F E` : structure registering a cover of prebundle trivializations
  and requiring that the relative transition maps are partial homeomorphisms.
* `FiberPrebundle.totalSpaceTopology a` : natural topology of the total space, making
  the prebundle into a bundle.

## Implementation notes

### Data vs mixins

For both fiber and vector bundles, one faces a choice: should the definition state the *existence*
of local trivializations (a propositional typeclass), or specify a fixed atlas of trivializations (a
typeclass containing data)?

In their initial mathlib implementations, both fiber and vector bundles were defined
propositionally. For vector bundles, this turns out to be mathematically wrong: in infinite
dimension, the transition function between two trivializations is not automatically continuous as a
map from the base `B` to the endomorphisms `F ‚ÜíL[R] F` of the fiber (considered with the
operator-norm topology), and so the definition needs to be modified by restricting consideration to
a family of trivializations (constituting the data) which are all mutually-compatible in this sense.
The PRs #13052 and #13175 implemented this change.

There is still the choice about whether to hold this data at the level of fiber bundles or of vector
bundles. As of PR #17505, the data is all held in `FiberBundle`, with `VectorBundle` a
(propositional) mixin stating fiberwise-linearity.

This allows bundles to carry instances of typeclasses in which the scalar field, `R`, does not
appear as a parameter. Notably, we would like a vector bundle over `R` with fiber `F` over base `B`
to be a `ChartedSpace (B √ó F)`, with the trivializations providing the charts. This would be a
dangerous instance for typeclass inference, because `R` does not appear as a parameter in
`ChartedSpace (B √ó F)`. But if the data of the trivializations is held in `FiberBundle`, then a
fiber bundle with fiber `F` over base `B` can be a `ChartedSpace (B √ó F)`, and this is safe for
typeclass inference.

We expect that this choice of definition will also streamline constructions of fiber bundles with
similar underlying structure (e.g., the same bundle being both a real and complex vector bundle).

### Core construction

A fiber bundle with fiber `F` over a base `B` is a family of spaces isomorphic to `F`,
indexed by `B`, which is locally trivial in the following sense: there is a covering of `B` by open
sets such that, on each such open set `s`, the bundle is isomorphic to `s √ó F`.

To construct a fiber bundle formally, the main data is what happens when one changes trivializations
from `s √ó F` to `s' √ó F` on `s ‚à© s'`: one should get a family of homeomorphisms of `F`, depending
continuously on the base point, satisfying basic compatibility conditions (cocycle property).
Useful classes of bundles can then be specified by requiring that these homeomorphisms of `F`
belong to some subgroup, preserving some structure (the "structure group of the bundle"): then
these structures are inherited by the fibers of the bundle.

Given such trivialization change data (encoded below in a structure called
`FiberBundleCore`), one can construct the fiber bundle. The intrinsic canonical
mathematical construction is the following.
The fiber above `x` is the disjoint union of `F` over all trivializations, modulo the gluing
identifications: one gets a fiber which is isomorphic to `F`, but non-canonically
(each choice of one of the trivializations around `x` gives such an isomorphism). Given a
trivialization over a set `s`, one gets an isomorphism between `s √ó F` and `proj^{-1} s`, by using
the identification corresponding to this trivialization. One chooses the topology on the bundle that
makes all of these into homeomorphisms.

For the practical implementation, it turns out to be more convenient to avoid completely the
gluing and quotienting construction above, and to declare above each `x` that the fiber is `F`,
but thinking that it corresponds to the `F` coming from the choice of one trivialization around `x`.
This has several practical advantages:
* without any work, one gets a topological space structure on the fiber. And if `F` has more
structure it is inherited for free by the fiber.
* In the case of the tangent bundle of manifolds, this implies that on vector spaces the derivative
(from `F` to `F`) and the manifold derivative (from `TangentSpace I x` to `TangentSpace I' (f x)`)
are equal.

A drawback is that some silly constructions will typecheck: in the case of the tangent bundle, one
can add two vectors in different tangent spaces (as they both are elements of `F` from the point of
view of Lean). To solve this, one could mark the tangent space as irreducible, but then one would
lose the identification of the tangent space to `F` with `F`. There is however a big advantage of
this situation: even if Lean can not check that two basepoints are defeq, it will accept the fact
that the tangent spaces are the same. For instance, if two maps `f` and `g` are locally inverse to
each other, one can express that the composition of their derivatives is the identity of
`TangentSpace I x`. One could fear issues as this composition goes from `TangentSpace I x` to
`TangentSpace I (g (f x))` (which should be the same, but should not be obvious to Lean
as it does not know that `g (f x) = x`). As these types are the same to Lean (equal to `F`), there
are in fact no dependent type difficulties here!

For this construction of a fiber bundle from a `FiberBundleCore`, we should thus
choose for each `x` one specific trivialization around it. We include this choice in the definition
of the `FiberBundleCore`, as it makes some constructions more
functorial and it is a nice way to say that the trivializations cover the whole space `B`.

With this definition, the type of the fiber bundle space constructed from the core data is
`Bundle.TotalSpace F (fun b : B ‚Ü¶ F)`, but the topology is not the product one, in general.

We also take the indexing type (indexing all the trivializations) as a parameter to the fiber bundle
core: it could always be taken as a subtype of all the maps from open subsets of `B` to continuous
maps of `F`, but in practice it will sometimes be something else. For instance, on a manifold, one
will use the set of charts as a good parameterization for the trivializations of the tangent bundle.
Or for the pullback of a `FiberBundleCore`, the indexing type will be the same as
for the initial bundle.

## Tags
Fiber bundle, topological bundle, structure group
-/


variable {Œπ B F X : Type*} [TopologicalSpace X]

open TopologicalSpace Filter Set Bundle Topology

/-! ### General definition of fiber bundles -/

section FiberBundle

variable (F) [TopologicalSpace B] [TopologicalSpace F] (E : B ‚Üí Type*)
  [TopologicalSpace (TotalSpace F E)] [‚àÄ b, TopologicalSpace (E b)]

/-- A (topological) fiber bundle with fiber `F` over a base `B` is a space projecting on `B`
for which the fibers are all homeomorphic to `F`, such that the local situation around each point
is a direct product. -/
class FiberBundle where
  totalSpaceMk_inducing' : ‚àÄ b : B, Inducing (@TotalSpace.mk B F E b)
  trivializationAtlas' : Set (Trivialization F (œÄ F E))
  trivializationAt' : B ‚Üí Trivialization F (œÄ F E)
  mem_baseSet_trivializationAt' : ‚àÄ b : B, b ‚àà (trivializationAt' b).baseSet
  trivialization_mem_atlas' : ‚àÄ b : B, trivializationAt' b ‚àà trivializationAtlas'
#align fiber_bundle FiberBundle

namespace FiberBundle

variable [FiberBundle F E] (b : B)

theorem totalSpaceMk_inducing : Inducing (@TotalSpace.mk B F E b) := totalSpaceMk_inducing' b

/-- Atlas of a fiber bundle. -/
abbrev trivializationAtlas : Set (Trivialization F (œÄ F E)) := trivializationAtlas'

/-- Trivialization of a fiber bundle at a point. -/
abbrev trivializationAt : Trivialization F (œÄ F E) := trivializationAt' b

theorem mem_baseSet_trivializationAt : b ‚àà (trivializationAt F E b).baseSet :=
  mem_baseSet_trivializationAt' b

theorem trivialization_mem_atlas : trivializationAt F E b ‚àà trivializationAtlas F E :=
  trivialization_mem_atlas' b

end FiberBundle

export FiberBundle (totalSpaceMk_inducing trivializationAtlas trivializationAt
  mem_baseSet_trivializationAt trivialization_mem_atlas)

variable {F E}

/-- Given a type `E` equipped with a fiber bundle structure, this is a `Prop` typeclass
for trivializations of `E`, expressing that a trivialization is in the designated atlas for the
bundle.  This is needed because lemmas about the linearity of trivializations or the continuity (as
functions to `F ‚ÜíL[R] F`, where `F` is the model fiber) of the transition functions are only
expected to hold for trivializations in the designated atlas. -/
@[mk_iff]
class MemTrivializationAtlas [FiberBundle F E] (e : Trivialization F (œÄ F E)) : Prop where
  out : e ‚àà trivializationAtlas F E
#align mem_trivialization_atlas MemTrivializationAtlas

instance [FiberBundle F E] (b : B) : MemTrivializationAtlas (trivializationAt F E b) where
  out := trivialization_mem_atlas F E b

namespace FiberBundle

variable (F)
variable [FiberBundle F E]

theorem map_proj_nhds (x : TotalSpace F E) : map (œÄ F E) (ùìù x) = ùìù x.proj :=
  (trivializationAt F E x.proj).map_proj_nhds <|
    (trivializationAt F E x.proj).mem_source.2 <| mem_baseSet_trivializationAt F E x.proj
#align fiber_bundle.map_proj_nhds FiberBundle.map_proj_nhds

variable (E)

/-- The projection from a fiber bundle to its base is continuous. -/
@[continuity]
theorem continuous_proj : Continuous (œÄ F E) :=
  continuous_iff_continuousAt.2 fun x => (map_proj_nhds F x).le
#align fiber_bundle.continuous_proj FiberBundle.continuous_proj

/-- The projection from a fiber bundle to its base is an open map. -/
theorem isOpenMap_proj : IsOpenMap (œÄ F E) :=
  IsOpenMap.of_nhds_le fun x => (map_proj_nhds F x).ge
#align fiber_bundle.is_open_map_proj FiberBundle.isOpenMap_proj

/-- The projection from a fiber bundle with a nonempty fiber to its base is a surjective
map. -/
theorem surjective_proj [Nonempty F] : Function.Surjective (œÄ F E) := fun b =>
  let ‚ü®p, _, hpb‚ü© :=
    (trivializationAt F E b).proj_surjOn_baseSet (mem_baseSet_trivializationAt F E b)
  ‚ü®p, hpb‚ü©
#align fiber_bundle.surjective_proj FiberBundle.surjective_proj

/-- The projection from a fiber bundle with a nonempty fiber to its base is a quotient
map. -/
theorem quotientMap_proj [Nonempty F] : QuotientMap (œÄ F E) :=
  (isOpenMap_proj F E).to_quotientMap (continuous_proj F E) (surjective_proj F E)
#align fiber_bundle.quotient_map_proj FiberBundle.quotientMap_proj

theorem continuous_totalSpaceMk (x : B) : Continuous (@TotalSpace.mk B F E x) :=
  (totalSpaceMk_inducing F E x).continuous
#align fiber_bundle.continuous_total_space_mk FiberBundle.continuous_totalSpaceMk

theorem totalSpaceMk_embedding (x : B) : Embedding (@TotalSpace.mk B F E x) :=
  ‚ü®totalSpaceMk_inducing F E x, TotalSpace.mk_injective x‚ü©

theorem totalSpaceMk_closedEmbedding [T1Space B] (x : B) :
    ClosedEmbedding (@TotalSpace.mk B F E x) :=
  ‚ü®totalSpaceMk_embedding F E x, by
    rw [TotalSpace.range_mk]
    exact isClosed_singleton.preimage <| continuous_proj F E‚ü©

variable {E F}

@[simp, mfld_simps]
theorem mem_trivializationAt_proj_source {x : TotalSpace F E} :
    x ‚àà (trivializationAt F E x.proj).source :=
  (Trivialization.mem_source _).mpr <| mem_baseSet_trivializationAt F E x.proj
#align fiber_bundle.mem_trivialization_at_proj_source FiberBundle.mem_trivializationAt_proj_source

-- Porting note: removed `@[simp, mfld_simps]` because `simp` could already prove this
theorem trivializationAt_proj_fst {x : TotalSpace F E} :
    ((trivializationAt F E x.proj) x).1 = x.proj :=
  Trivialization.coe_fst' _ <| mem_baseSet_trivializationAt F E x.proj
#align fiber_bundle.trivialization_at_proj_fst FiberBundle.trivializationAt_proj_fst

variable (F)

open Trivialization

/-- Characterization of continuous functions (at a point, within a set) into a fiber bundle. -/
theorem continuousWithinAt_totalSpace (f : X ‚Üí TotalSpace F E) {s : Set X} {x‚ÇÄ : X} :
    ContinuousWithinAt f s x‚ÇÄ ‚Üî
      ContinuousWithinAt (fun x => (f x).proj) s x‚ÇÄ ‚àß
        ContinuousWithinAt (fun x => ((trivializationAt F E (f x‚ÇÄ).proj) (f x)).2) s x‚ÇÄ :=
  (trivializationAt F E (f x‚ÇÄ).proj).tendsto_nhds_iff mem_trivializationAt_proj_source
#align fiber_bundle.continuous_within_at_total_space FiberBundle.continuousWithinAt_totalSpace

/-- Characterization of continuous functions (at a point) into a fiber bundle. -/
theorem continuousAt_totalSpace (f : X ‚Üí TotalSpace F E) {x‚ÇÄ : X} :
    ContinuousAt f x‚ÇÄ ‚Üî
      ContinuousAt (fun x => (f x).proj) x‚ÇÄ ‚àß
        ContinuousAt (fun x => ((trivializationAt F E (f x‚ÇÄ).proj) (f x)).2) x‚ÇÄ :=
  (trivializationAt F E (f x‚ÇÄ).proj).tendsto_nhds_iff mem_trivializationAt_proj_source
#align fiber_bundle.continuous_at_total_space FiberBundle.continuousAt_totalSpace

end FiberBundle

variable (F E)

/-- If `E` is a fiber bundle over a conditionally complete linear order,
then it is trivial over any closed interval. -/
theorem FiberBundle.exists_trivialization_Icc_subset [ConditionallyCompleteLinearOrder B]
    [OrderTopology B] [FiberBundle F E] (a b : B) :
    ‚àÉ e : Trivialization F (œÄ F E), Icc a b ‚äÜ e.baseSet := by
  obtain ‚ü®ea, hea‚ü© : ‚àÉ ea : Trivialization F (œÄ F E), a ‚àà ea.baseSet :=
    ‚ü®trivializationAt F E a, mem_baseSet_trivializationAt F E a‚ü©
  -- If `a < b`, then `[a, b] = ‚àÖ`, and the statement is trivial
  cases' lt_or_le b a with hab hab
  ¬∑ exact ‚ü®ea, by simp [*]‚ü©
  /- Let `s` be the set of points `x ‚àà [a, b]` such that `E` is trivializable over `[a, x]`.
    We need to show that `b ‚àà s`. Let `c = Sup s`. We will show that `c ‚àà s` and `c = b`. -/
  set s : Set B := { x ‚àà Icc a b | ‚àÉ e : Trivialization F (œÄ F E), Icc a x ‚äÜ e.baseSet }
  have ha : a ‚àà s := ‚ü®left_mem_Icc.2 hab, ea, by simp [hea]‚ü©
  have sne : s.Nonempty := ‚ü®a, ha‚ü©
  have hsb : b ‚àà upperBounds s := fun x hx => hx.1.2
  have sbd : BddAbove s := ‚ü®b, hsb‚ü©
  set c := sSup s
  have hsc : IsLUB s c := isLUB_csSup sne sbd
  have hc : c ‚àà Icc a b := ‚ü®hsc.1 ha, hsc.2 hsb‚ü©
  obtain ‚ü®-, ec : Trivialization F (œÄ F E), hec : Icc a c ‚äÜ ec.baseSet‚ü© : c ‚àà s := by
    rcases hc.1.eq_or_lt with heq | hlt
    ¬∑ rwa [‚Üê heq]
    refine ‚ü®hc, ?_‚ü©
    /- In order to show that `c ‚àà s`, consider a trivialization `ec` of `proj` over a neighborhood
      of `c`. Its base set includes `(c', c]` for some `c' ‚àà [a, c)`. -/
    obtain ‚ü®ec, hc‚ü© : ‚àÉ ec : Trivialization F (œÄ F E), c ‚àà ec.baseSet :=
      ‚ü®trivializationAt F E c, mem_baseSet_trivializationAt F E c‚ü©
    obtain ‚ü®c', hc', hc'e‚ü© : ‚àÉ c' ‚àà Ico a c, Ioc c' c ‚äÜ ec.baseSet :=
      (mem_nhdsWithin_Iic_iff_exists_mem_Ico_Ioc_subset hlt).1
        (mem_nhdsWithin_of_mem_nhds <| IsOpen.mem_nhds ec.open_baseSet hc)
    /- Since `c' < c = Sup s`, there exists `d ‚àà s ‚à© (c', c]`. Let `ead` be a trivialization of
      `proj` over `[a, d]`. Then we can glue `ead` and `ec` into a trivialization over `[a, c]`. -/
    obtain ‚ü®d, ‚ü®hdab, ead, had‚ü©, hd‚ü© : ‚àÉ d ‚àà s, d ‚àà Ioc c' c := hsc.exists_between hc'.2
    refine ‚ü®ead.piecewiseLe ec d (had ‚ü®hdab.1, le_rfl‚ü©) (hc'e hd), subset_ite.2 ?_‚ü©
    exact ‚ü®fun x hx => had ‚ü®hx.1.1, hx.2‚ü©, fun x hx => hc'e ‚ü®hd.1.trans (not_le.1 hx.2), hx.1.2‚ü©‚ü©
  /- So, `c ‚àà s`. Let `ec` be a trivialization of `proj` over `[a, c]`.  If `c = b`, then we are
    done. Otherwise we show that `proj` can be trivialized over a larger interval `[a, d]`,
    `d ‚àà (c, b]`, hence `c` is not an upper bound of `s`. -/
  rcases hc.2.eq_or_lt with heq | hlt
  ¬∑ exact ‚ü®ec, heq ‚ñ∏ hec‚ü©
  rsuffices ‚ü®d, hdcb, hd‚ü© : ‚àÉ d ‚àà Ioc c b, ‚àÉ e : Trivialization F (œÄ F E), Icc a d ‚äÜ e.baseSet
  ¬∑ exact ((hsc.1 ‚ü®‚ü®hc.1.trans hdcb.1.le, hdcb.2‚ü©, hd‚ü©).not_lt hdcb.1).elim
  /- Since the base set of `ec` is open, it includes `[c, d)` (hence, `[a, d)`) for some
    `d ‚àà (c, b]`. -/
  obtain ‚ü®d, hdcb, hd‚ü© : ‚àÉ d ‚àà Ioc c b, Ico c d ‚äÜ ec.baseSet :=
    (mem_nhdsWithin_Ici_iff_exists_mem_Ioc_Ico_subset hlt).1
      (mem_nhdsWithin_of_mem_nhds <| IsOpen.mem_nhds ec.open_baseSet (hec ‚ü®hc.1, le_rfl‚ü©))
  have had : Ico a d ‚äÜ ec.baseSet := Ico_subset_Icc_union_Ico.trans (union_subset hec hd)
  by_cases he : Disjoint (Iio d) (Ioi c)
  ¬∑ /- If `(c, d) = ‚àÖ`, then let `ed` be a trivialization of `proj` over a neighborhood of `d`.
      Then the disjoint union of `ec` restricted to `(-‚àû, d)` and `ed` restricted to `(c, ‚àû)` is
      a trivialization over `[a, d]`. -/
    obtain ‚ü®ed, hed‚ü© : ‚àÉ ed : Trivialization F (œÄ F E), d ‚àà ed.baseSet :=
      ‚ü®trivializationAt F E d, mem_baseSet_trivializationAt F E d‚ü©
    refine ‚ü®d, hdcb,
      (ec.restrOpen (Iio d) isOpen_Iio).disjointUnion (ed.restrOpen (Ioi c) isOpen_Ioi)
        (he.mono (inter_subset_right _ _) (inter_subset_right _ _)), fun x hx => ?_‚ü©
    rcases hx.2.eq_or_lt with (rfl | hxd)
    exacts [Or.inr ‚ü®hed, hdcb.1‚ü©, Or.inl ‚ü®had ‚ü®hx.1, hxd‚ü©, hxd‚ü©]
  ¬∑ /- If `(c, d)` is nonempty, then take `d' ‚àà (c, d)`. Since the base set of `ec` includes
          `[a, d)`, it includes `[a, d'] ‚äÜ [a, d)` as well. -/
    rw [disjoint_left] at he
    push_neg at he
    rcases he with ‚ü®d', hdd' : d' < d, hd'c‚ü©
    exact ‚ü®d', ‚ü®hd'c, hdd'.le.trans hdcb.2‚ü©, ec, (Icc_subset_Ico_right hdd').trans had‚ü©
#align fiber_bundle.exists_trivialization_Icc_subset FiberBundle.exists_trivialization_Icc_subset

end FiberBundle

/-! ### Core construction for constructing fiber bundles -/

/-- Core data defining a locally trivial bundle with fiber `F` over a topological
space `B`. Note that "bundle" is used in its mathematical sense. This is the (computer science)
bundled version, i.e., all the relevant data is contained in the following structure. A family of
local trivializations is indexed by a type `Œπ`, on open subsets `baseSet i` for each `i : Œπ`.
Trivialization changes from `i` to `j` are given by continuous maps `coordChange i j` from
`baseSet i ‚à© baseSet j` to the set of homeomorphisms of `F`, but we express them as maps
`B ‚Üí F ‚Üí F` and require continuity on `(baseSet i ‚à© baseSet j) √ó F` to avoid the topology on the
space of continuous maps on `F`. -/
-- Porting note(#5171): was @[nolint has_nonempty_instance]
structure FiberBundleCore (Œπ : Type*) (B : Type*) [TopologicalSpace B] (F : Type*)
    [TopologicalSpace F] where
  baseSet : Œπ ‚Üí Set B
  isOpen_baseSet : ‚àÄ i, IsOpen (baseSet i)
  indexAt : B ‚Üí Œπ
  mem_baseSet_at : ‚àÄ x, x ‚àà baseSet (indexAt x)
  coordChange : Œπ ‚Üí Œπ ‚Üí B ‚Üí F ‚Üí F
  coordChange_self : ‚àÄ i, ‚àÄ x ‚àà baseSet i, ‚àÄ v, coordChange i i x v = v
  continuousOn_coordChange : ‚àÄ i j,
    ContinuousOn (fun p : B √ó F => coordChange i j p.1 p.2) ((baseSet i ‚à© baseSet j) √óÀ¢ univ)
  coordChange_comp : ‚àÄ i j k, ‚àÄ x ‚àà baseSet i ‚à© baseSet j ‚à© baseSet k, ‚àÄ v,
    (coordChange j k x) (coordChange i j x v) = coordChange i k x v
#align fiber_bundle_core FiberBundleCore

namespace FiberBundleCore

variable [TopologicalSpace B] [TopologicalSpace F] (Z : FiberBundleCore Œπ B F)

/-- The index set of a fiber bundle core, as a convenience function for dot notation -/
@[nolint unusedArguments] -- Porting note(#5171): was has_nonempty_instance
def Index (_Z : FiberBundleCore Œπ B F) := Œπ
#align fiber_bundle_core.index FiberBundleCore.Index

/-- The base space of a fiber bundle core, as a convenience function for dot notation -/
@[nolint unusedArguments, reducible]
def Base (_Z : FiberBundleCore Œπ B F) := B
#align fiber_bundle_core.base FiberBundleCore.Base

/-- The fiber of a fiber bundle core, as a convenience function for dot notation and
typeclass inference -/
@[nolint unusedArguments] -- Porting note(#5171): was has_nonempty_instance
def Fiber (_ : FiberBundleCore Œπ B F) (_x : B) := F
#align fiber_bundle_core.fiber FiberBundleCore.Fiber

instance topologicalSpaceFiber (x : B) : TopologicalSpace (Z.Fiber x) := ‚Äπ_‚Ä∫
#align fiber_bundle_core.topological_space_fiber FiberBundleCore.topologicalSpaceFiber

/-- The total space of the fiber bundle, as a convenience function for dot notation.
It is by definition equal to `Bundle.TotalSpace F Z.Fiber`. -/
abbrev TotalSpace := Bundle.TotalSpace F Z.Fiber
#align fiber_bundle_core.total_space FiberBundleCore.TotalSpace

/-- The projection from the total space of a fiber bundle core, on its base. -/
@[reducible, simp, mfld_simps]
def proj : Z.TotalSpace ‚Üí B :=
  Bundle.TotalSpace.proj
#align fiber_bundle_core.proj FiberBundleCore.proj

/-- Local homeomorphism version of the trivialization change. -/
def trivChange (i j : Œπ) : PartialHomeomorph (B √ó F) (B √ó F) where
  source := (Z.baseSet i ‚à© Z.baseSet j) √óÀ¢ univ
  target := (Z.baseSet i ‚à© Z.baseSet j) √óÀ¢ univ
  toFun p := ‚ü®p.1, Z.coordChange i j p.1 p.2‚ü©
  invFun p := ‚ü®p.1, Z.coordChange j i p.1 p.2‚ü©
  map_source' p hp := by simpa using hp
  map_target' p hp := by simpa using hp
  left_inv' := by
    rintro ‚ü®x, v‚ü© hx
    simp only [prod_mk_mem_set_prod_eq, mem_inter_iff, and_true, mem_univ] at hx
    dsimp only
    rw [coordChange_comp, Z.coordChange_self]
    exacts [hx.1, ‚ü®‚ü®hx.1, hx.2‚ü©, hx.1‚ü©]
  right_inv' := by
    rintro ‚ü®x, v‚ü© hx
    simp only [prod_mk_mem_set_prod_eq, mem_inter_iff, and_true_iff, mem_univ] at hx
    dsimp only
    rw [Z.coordChange_comp, Z.coordChange_self]
    ¬∑ exact hx.2
    ¬∑ simp [hx]
  open_source := ((Z.isOpen_baseSet i).inter (Z.isOpen_baseSet j)).prod isOpen_univ
  open_target := ((Z.isOpen_baseSet i).inter (Z.isOpen_baseSet j)).prod isOpen_univ
  continuousOn_toFun := continuous_fst.continuousOn.prod (Z.continuousOn_coordChange i j)
  continuousOn_invFun := by
    simpa [inter_comm] using continuous_fst.continuousOn.prod (Z.continuousOn_coordChange j i)
#align fiber_bundle_core.triv_change FiberBundleCore.trivChange

@[simp, mfld_simps]
theorem mem_trivChange_source (i j : Œπ) (p : B √ó F) :
    p ‚àà (Z.trivChange i j).source ‚Üî p.1 ‚àà Z.baseSet i ‚à© Z.baseSet j := by
  erw [mem_prod]
  simp
#align fiber_bundle_core.mem_triv_change_source FiberBundleCore.mem_trivChange_source

/-- Associate to a trivialization index `i : Œπ` the corresponding trivialization, i.e., a bijection
between `proj ‚Åª¬π (baseSet i)` and `baseSet i √ó F`. As the fiber above `x` is `F` but read in the
chart with index `index_at x`, the trivialization in the fiber above x is by definition the
coordinate change from i to `index_at x`, so it depends on `x`.
The local trivialization will ultimately be a partial homeomorphism. For now, we only introduce the
partial equivalence version, denoted with a prime.
In further developments, avoid this auxiliary version, and use `Z.local_triv` instead. -/
def localTrivAsPartialEquiv (i : Œπ) : PartialEquiv Z.TotalSpace (B √ó F) where
  source := Z.proj ‚Åª¬π' Z.baseSet i
  target := Z.baseSet i √óÀ¢ univ
  invFun p := ‚ü®p.1, Z.coordChange i (Z.indexAt p.1) p.1 p.2‚ü©
  toFun p := ‚ü®p.1, Z.coordChange (Z.indexAt p.1) i p.1 p.2‚ü©
  map_source' p hp := by
    simpa only [Set.mem_preimage, and_true_iff, Set.mem_univ, Set.prod_mk_mem_set_prod_eq] using hp
  map_target' p hp := by
    simpa only [Set.mem_preimage, and_true_iff, Set.mem_univ, Set.mem_prod] using hp
  left_inv' := by
    rintro ‚ü®x, v‚ü© hx
    replace hx : x ‚àà Z.baseSet i := hx
    dsimp only
    rw [Z.coordChange_comp, Z.coordChange_self] <;> apply_rules [mem_baseSet_at, mem_inter]
  right_inv' := by
    rintro ‚ü®x, v‚ü© hx
    simp only [prod_mk_mem_set_prod_eq, and_true_iff, mem_univ] at hx
    dsimp only
    rw [Z.coordChange_comp, Z.coordChange_self]
    exacts [hx, ‚ü®‚ü®hx, Z.mem_baseSet_at _‚ü©, hx‚ü©]
#align fiber_bundle_core.local_triv_as_local_equiv FiberBundleCore.localTrivAsPartialEquiv

variable (i : Œπ)

theorem mem_localTrivAsPartialEquiv_source (p : Z.TotalSpace) :
    p ‚àà (Z.localTrivAsPartialEquiv i).source ‚Üî p.1 ‚àà Z.baseSet i :=
  Iff.rfl
#align fiber_bundle_core.mem_local_triv_as_local_equiv_source FiberBundleCore.mem_localTrivAsPartialEquiv_source

theorem mem_localTrivAsPartialEquiv_target (p : B √ó F) :
    p ‚àà (Z.localTrivAsPartialEquiv i).target ‚Üî p.1 ‚àà Z.baseSet i := by
  erw [mem_prod]
  simp only [and_true_iff, mem_univ]
#align fiber_bundle_core.mem_local_triv_as_local_equiv_target FiberBundleCore.mem_localTrivAsPartialEquiv_target

theorem localTrivAsPartialEquiv_apply (p : Z.TotalSpace) :
    (Z.localTrivAsPartialEquiv i) p = ‚ü®p.1, Z.coordChange (Z.indexAt p.1) i p.1 p.2‚ü© :=
  rfl
#align fiber_bundle_core.local_triv_as_local_equiv_apply FiberBundleCore.localTrivAsPartialEquiv_apply

/-- The composition of two local trivializations is the trivialization change Z.triv_change i j. -/
theorem localTrivAsPartialEquiv_trans (i j : Œπ) :
    (Z.localTrivAsPartialEquiv i).symm.trans (Z.localTrivAsPartialEquiv j) ‚âà
      (Z.trivChange i j).toPartialEquiv := by
  constructor
  ¬∑ ext x
    simp only [mem_localTrivAsPartialEquiv_target, mfld_simps]
    rfl
  ¬∑ rintro ‚ü®x, v‚ü© hx
    simp only [trivChange, localTrivAsPartialEquiv, PartialEquiv.symm, true_and_iff,
      Prod.mk.inj_iff, prod_mk_mem_set_prod_eq, PartialEquiv.trans_source, mem_inter_iff,
      and_true_iff, mem_preimage, proj, mem_univ, eq_self_iff_true, (¬∑ ‚àò ¬∑),
      PartialEquiv.coe_trans, TotalSpace.proj] at hx ‚ä¢
    simp only [Z.coordChange_comp, hx, mem_inter_iff, and_self_iff, mem_baseSet_at]
#align fiber_bundle_core.local_triv_as_local_equiv_trans FiberBundleCore.localTrivAsPartialEquiv_trans

/-- Topological structure on the total space of a fiber bundle created from core, designed so
that all the local trivialization are continuous. -/
instance toTopologicalSpace : TopologicalSpace (Bundle.TotalSpace F Z.Fiber) :=
  TopologicalSpace.generateFrom <| ‚ãÉ (i : Œπ) (s : Set (B √ó F)) (_ : IsOpen s),
    {(Z.localTrivAsPartialEquiv i).source ‚à© Z.localTrivAsPartialEquiv i ‚Åª¬π' s}
#align fiber_bundle_core.to_topological_space FiberBundleCore.toTopologicalSpace

variable (b : B) (a : F)

theorem open_source' (i : Œπ) : IsOpen (Z.localTrivAsPartialEquiv i).source := by
  apply TopologicalSpace.GenerateOpen.basic
  simp only [exists_prop, mem_iUnion, mem_singleton_iff]
  refine ‚ü®i, Z.baseSet i √óÀ¢ univ, (Z.isOpen_baseSet i).prod isOpen_univ, ?_‚ü©
  ext p
  simp only [localTrivAsPartialEquiv_apply, prod_mk_mem_set_prod_eq, mem_inter_iff, and_self_iff,
    mem_localTrivAsPartialEquiv_source, and_true, mem_univ, mem_preimage]
#align fiber_bundle_core.open_source' FiberBundleCore.open_source'

/-- Extended version of the local trivialization of a fiber bundle constructed from core,
registering additionally in its type that it is a local bundle trivialization. -/
def localTriv (i : Œπ) : Trivialization F Z.proj where
  baseSet := Z.baseSet i
  open_baseSet := Z.isOpen_baseSet i
  source_eq := rfl
  target_eq := rfl
  proj_toFun p _ := by
    simp only [mfld_simps]
    rfl
  open_source := Z.open_source' i
  open_target := (Z.isOpen_baseSet i).prod isOpen_univ
  continuousOn_toFun := by
    rw [continuousOn_open_iff (Z.open_source' i)]
    intro s s_open
    apply TopologicalSpace.GenerateOpen.basic
    simp only [exists_prop, mem_iUnion, mem_singleton_iff]
    exact ‚ü®i, s, s_open, rfl‚ü©
  continuousOn_invFun := by
    refine continuousOn_isOpen_of_generateFrom fun t ht ‚Ü¶ ?_
    simp only [exists_prop, mem_iUnion, mem_singleton_iff] at ht
    obtain ‚ü®j, s, s_open, ts‚ü© : ‚àÉ j s, IsOpen s ‚àß
      t = (localTrivAsPartialEquiv Z j).source ‚à© localTrivAsPartialEquiv Z j ‚Åª¬π' s := ht
    rw [ts]
    simp only [PartialEquiv.right_inv, preimage_inter, PartialEquiv.left_inv]
    let e := Z.localTrivAsPartialEquiv i
    let e' := Z.localTrivAsPartialEquiv j
    let f := e.symm.trans e'
    have : IsOpen (f.source ‚à© f ‚Åª¬π' s) := by
      rw [PartialEquiv.EqOnSource.source_inter_preimage_eq (Z.localTrivAsPartialEquiv_trans i j)]
      exact (continuousOn_open_iff (Z.trivChange i j).open_source).1
        (Z.trivChange i j).continuousOn _ s_open
    convert this using 1
    dsimp [f, PartialEquiv.trans_source]
    rw [‚Üê preimage_comp, inter_assoc]
  toPartialEquiv := Z.localTrivAsPartialEquiv i
#align fiber_bundle_core.local_triv FiberBundleCore.localTriv

/-- Preferred local trivialization of a fiber bundle constructed from core, at a given point, as
a bundle trivialization -/
def localTrivAt (b : B) : Trivialization F (œÄ F Z.Fiber) :=
  Z.localTriv (Z.indexAt b)
#align fiber_bundle_core.local_triv_at FiberBundleCore.localTrivAt

@[simp, mfld_simps]
theorem localTrivAt_def (b : B) : Z.localTriv (Z.indexAt b) = Z.localTrivAt b :=
  rfl
#align fiber_bundle_core.local_triv_at_def FiberBundleCore.localTrivAt_def

theorem localTrivAt_snd (b : B) (p) :
    (Z.localTrivAt b p).2 = Z.coordChange (Z.indexAt p.1) (Z.indexAt b) p.1 p.2 :=
  rfl

/-- If an element of `F` is invariant under all coordinate changes, then one can define a
corresponding section of the fiber bundle, which is continuous. This applies in particular to the
zero section of a vector bundle. Another example (not yet defined) would be the identity
section of the endomorphism bundle of a vector bundle. -/
theorem continuous_const_section (v : F)
    (h : ‚àÄ i j, ‚àÄ x ‚àà Z.baseSet i ‚à© Z.baseSet j, Z.coordChange i j x v = v) :
    Continuous (show B ‚Üí Z.TotalSpace from fun x => ‚ü®x, v‚ü©) := by
  refine continuous_iff_continuousAt.2 fun x => ?_
  have A : Z.baseSet (Z.indexAt x) ‚àà ùìù x :=
    IsOpen.mem_nhds (Z.isOpen_baseSet (Z.indexAt x)) (Z.mem_baseSet_at x)
  refine ((Z.localTrivAt x).toPartialHomeomorph.continuousAt_iff_continuousAt_comp_left ?_).2 ?_
  ¬∑ exact A
  ¬∑ apply continuousAt_id.prod
    simp only [(¬∑ ‚àò ¬∑), mfld_simps, localTrivAt_snd]
    have : ContinuousOn (fun _ : B => v) (Z.baseSet (Z.indexAt x)) := continuousOn_const
    refine (this.congr fun y hy ‚Ü¶ ?_).continuousAt A
    exact h _ _ _ ‚ü®mem_baseSet_at _ _, hy‚ü©
#align fiber_bundle_core.continuous_const_section FiberBundleCore.continuous_const_section

@[simp, mfld_simps]
theorem localTrivAsPartialEquiv_coe : ‚áë(Z.localTrivAsPartialEquiv i) = Z.localTriv i :=
  rfl
#align fiber_bundle_core.local_triv_as_local_equiv_coe FiberBundleCore.localTrivAsPartialEquiv_coe

@[simp, mfld_simps]
theorem localTrivAsPartialEquiv_source :
    (Z.localTrivAsPartialEquiv i).source = (Z.localTriv i).source :=
  rfl
#align fiber_bundle_core.local_triv_as_local_equiv_source FiberBundleCore.localTrivAsPartialEquiv_source

@[simp, mfld_simps]
theorem localTrivAsPartialEquiv_target :
    (Z.localTrivAsPartialEquiv i).target = (Z.localTriv i).target :=
  rfl
#align fiber_bundle_core.local_triv_as_local_equiv_target FiberBundleCore.localTrivAsPartialEquiv_target

@[simp, mfld_simps]
theorem localTrivAsPartialEquiv_symm :
    (Z.localTrivAsPartialEquiv i).symm = (Z.localTriv i).toPartialEquiv.symm :=
  rfl
#align fiber_bundle_core.local_triv_as_local_equiv_symm FiberBundleCore.localTrivAsPartialEquiv_symm

@[simp, mfld_simps]
theorem baseSet_at : Z.baseSet i = (Z.localTriv i).baseSet :=
  rfl
#align fiber_bundle_core.base_set_at FiberBundleCore.baseSet_at

@[simp, mfld_simps]
theorem localTriv_apply (p : Z.TotalSpace) :
    (Z.localTriv i) p = ‚ü®p.1, Z.coordChange (Z.indexAt p.1) i p.1 p.2‚ü© :=
  rfl
#align fiber_bundle_core.local_triv_apply FiberBundleCore.localTriv_apply

@[simp, mfld_simps]
theorem localTrivAt_apply (p : Z.TotalSpace) : (Z.localTrivAt p.1) p = ‚ü®p.1, p.2‚ü© := by
  rw [localTrivAt, localTriv_apply, coordChange_self]
  exact Z.mem_baseSet_at p.1
#align fiber_bundle_core.local_triv_at_apply FiberBundleCore.localTrivAt_apply

@[simp, mfld_simps]
theorem localTrivAt_apply_mk (b : B) (a : F) : (Z.localTrivAt b) ‚ü®b, a‚ü© = ‚ü®b, a‚ü© :=
  Z.localTrivAt_apply _
#align fiber_bundle_core.local_triv_at_apply_mk FiberBundleCore.localTrivAt_apply_mk

@[simp, mfld_simps]
theorem mem_localTriv_source (p : Z.TotalSpace) :
    p ‚àà (Z.localTriv i).source ‚Üî p.1 ‚àà (Z.localTriv i).baseSet :=
  Iff.rfl
#align fiber_bundle_core.mem_local_triv_source FiberBundleCore.mem_localTriv_source

@[simp, mfld_simps]
theorem mem_localTrivAt_source (p : Z.TotalSpace) (b : B) :
    p ‚àà (Z.localTrivAt b).source ‚Üî p.1 ‚àà (Z.localTrivAt b).baseSet :=
  Iff.rfl
#align fiber_bundle_core.mem_local_triv_at_source FiberBundleCore.mem_localTrivAt_source

@[simp, mfld_simps]
theorem mem_localTriv_target (p : B √ó F) :
    p ‚àà (Z.localTriv i).target ‚Üî p.1 ‚àà (Z.localTriv i).baseSet :=
  Trivialization.mem_target _
#align fiber_bundle_core.mem_local_triv_target FiberBundleCore.mem_localTriv_target

@[simp, mfld_simps]
theorem mem_localTrivAt_target (p : B √ó F) (b : B) :
    p ‚àà (Z.localTrivAt b).target ‚Üî p.1 ‚àà (Z.localTrivAt b).baseSet :=
  Trivialization.mem_target _
#align fiber_bundle_core.mem_local_triv_at_target FiberBundleCore.mem_localTrivAt_target

@[simp, mfld_simps]
theorem localTriv_symm_apply (p : B √ó F) :
    (Z.localTriv i).toPartialHomeomorph.symm p = ‚ü®p.1, Z.coordChange i (Z.indexAt p.1) p.1 p.2‚ü© :=
  rfl
#align fiber_bundle_core.local_triv_symm_apply FiberBundleCore.localTriv_symm_apply

@[simp, mfld_simps]
theorem mem_localTrivAt_baseSet (b : B) : b ‚àà (Z.localTrivAt b).baseSet := by
  rw [localTrivAt, ‚Üê baseSet_at]
  exact Z.mem_baseSet_at b
#align fiber_bundle_core.mem_local_triv_at_base_set FiberBundleCore.mem_localTrivAt_baseSet

-- Porting note (#10618): was @[simp, mfld_simps], now `simp` can prove it
theorem mk_mem_localTrivAt_source : (‚ü®b, a‚ü© : Z.TotalSpace) ‚àà (Z.localTrivAt b).source := by
  simp only [mfld_simps]
#align fiber_bundle_core.mem_source_at FiberBundleCore.mem_localTrivAt_source

/-- A fiber bundle constructed from core is indeed a fiber bundle. -/
instance fiberBundle : FiberBundle F Z.Fiber where
  totalSpaceMk_inducing' b := inducing_iff_nhds.2 fun x ‚Ü¶ by
    rw [(Z.localTrivAt b).nhds_eq_comap_inf_principal (mk_mem_localTrivAt_source _ _ _), comap_inf,
      comap_principal, comap_comap]
    simp only [(¬∑ ‚àò ¬∑), localTrivAt_apply_mk, Trivialization.coe_coe,
      ‚Üê (embedding_prod_mk b).nhds_eq_comap]
    convert_to ùìù x = ùìù x ‚äì ùìü univ
    ¬∑ congr
      exact eq_univ_of_forall (mk_mem_localTrivAt_source Z _)
    ¬∑ rw [principal_univ, inf_top_eq]
  trivializationAtlas' := Set.range Z.localTriv
  trivializationAt' := Z.localTrivAt
  mem_baseSet_trivializationAt' := Z.mem_baseSet_at
  trivialization_mem_atlas' b := ‚ü®Z.indexAt b, rfl‚ü©
#align fiber_bundle_core.fiber_bundle FiberBundleCore.fiberBundle

/-- The inclusion of a fiber into the total space is a continuous map. -/
@[continuity]
theorem continuous_totalSpaceMk (b : B) :
    Continuous (TotalSpace.mk b : Z.Fiber b ‚Üí Bundle.TotalSpace F Z.Fiber) :=
  FiberBundle.continuous_totalSpaceMk F Z.Fiber b
#align fiber_bundle_core.continuous_total_space_mk FiberBundleCore.continuous_totalSpaceMk

/-- The projection on the base of a fiber bundle created from core is continuous -/
nonrec theorem continuous_proj : Continuous Z.proj :=
  FiberBundle.continuous_proj F Z.Fiber
#align fiber_bundle_core.continuous_proj FiberBundleCore.continuous_proj

/-- The projection on the base of a fiber bundle created from core is an open map -/
nonrec theorem isOpenMap_proj : IsOpenMap Z.proj :=
  FiberBundle.isOpenMap_proj F Z.Fiber
#align fiber_bundle_core.is_open_map_proj FiberBundleCore.isOpenMap_proj

end FiberBundleCore

/-! ### Prebundle construction for constructing fiber bundles -/

variable (F) (E : B ‚Üí Type*) [TopologicalSpace B] [TopologicalSpace F]
  [‚àÄ x, TopologicalSpace (E x)]

/-- This structure permits to define a fiber bundle when trivializations are given as local
equivalences but there is not yet a topology on the total space. The total space is hence given a
topology in such a way that there is a fiber bundle structure for which the partial equivalences
are also partial homeomorphisms and hence local trivializations. -/
-- Porting note (#5171): was @[nolint has_nonempty_instance]
structure FiberPrebundle where
  pretrivializationAtlas : Set (Pretrivialization F (œÄ F E))
  pretrivializationAt : B ‚Üí Pretrivialization F (œÄ F E)
  mem_base_pretrivializationAt : ‚àÄ x : B, x ‚àà (pretrivializationAt x).baseSet
  pretrivialization_mem_atlas : ‚àÄ x : B, pretrivializationAt x ‚àà pretrivializationAtlas
  continuous_trivChange : ‚àÄ e, e ‚àà pretrivializationAtlas ‚Üí ‚àÄ e', e' ‚àà pretrivializationAtlas ‚Üí
    ContinuousOn (e ‚àò e'.toPartialEquiv.symm) (e'.target ‚à© e'.toPartialEquiv.symm ‚Åª¬π' e.source)
  totalSpaceMk_inducing : ‚àÄ b : B, Inducing (pretrivializationAt b ‚àò TotalSpace.mk b)
#align fiber_prebundle FiberPrebundle

namespace FiberPrebundle

variable {F E}
variable (a : FiberPrebundle F E) {e : Pretrivialization F (œÄ F E)}

/-- Topology on the total space that will make the prebundle into a bundle. -/
def totalSpaceTopology (a : FiberPrebundle F E) : TopologicalSpace (TotalSpace F E) :=
  ‚®Ü (e : Pretrivialization F (œÄ F E)) (_ : e ‚àà a.pretrivializationAtlas),
    coinduced e.setSymm instTopologicalSpaceSubtype
#align fiber_prebundle.total_space_topology FiberPrebundle.totalSpaceTopology

theorem continuous_symm_of_mem_pretrivializationAtlas (he : e ‚àà a.pretrivializationAtlas) :
    @ContinuousOn _ _ _ a.totalSpaceTopology e.toPartialEquiv.symm e.target := by
  refine fun z H U h => preimage_nhdsWithin_coinduced' H (le_def.1 (nhds_mono ?_) U h)
  exact le_iSup‚ÇÇ (Œ± := TopologicalSpace (TotalSpace F E)) e he
#align fiber_prebundle.continuous_symm_of_mem_pretrivialization_atlas FiberPrebundle.continuous_symm_of_mem_pretrivializationAtlas

theorem isOpen_source (e : Pretrivialization F (œÄ F E)) :
    IsOpen[a.totalSpaceTopology] e.source := by
  refine isOpen_iSup_iff.mpr fun e' => isOpen_iSup_iff.mpr fun _ => ?_
  refine isOpen_coinduced.mpr (isOpen_induced_iff.mpr ‚ü®e.target, e.open_target, ?_‚ü©)
  ext ‚ü®x, hx‚ü©
  simp only [mem_preimage, Pretrivialization.setSymm, restrict, e.mem_target, e.mem_source,
    e'.proj_symm_apply hx]
#align fiber_prebundle.is_open_source FiberPrebundle.isOpen_source

theorem isOpen_target_of_mem_pretrivializationAtlas_inter (e e' : Pretrivialization F (œÄ F E))
    (he' : e' ‚àà a.pretrivializationAtlas) :
    IsOpen (e'.toPartialEquiv.target ‚à© e'.toPartialEquiv.symm ‚Åª¬π' e.source) := by
  letI := a.totalSpaceTopology
  obtain ‚ü®u, hu1, hu2‚ü© := continuousOn_iff'.mp (a.continuous_symm_of_mem_pretrivializationAtlas he')
    e.source (a.isOpen_source e)
  rw [inter_comm, hu2]
  exact hu1.inter e'.open_target
#align fiber_prebundle.is_open_target_of_mem_pretrivialization_atlas_inter FiberPrebundle.isOpen_target_of_mem_pretrivializationAtlas_inter

/-- Promotion from a `Pretrivialization` to a `Trivialization`. -/
def trivializationOfMemPretrivializationAtlas (he : e ‚àà a.pretrivializationAtlas) :
    @Trivialization B F _ _ _ a.totalSpaceTopology (œÄ F E) :=
  let _ := a.totalSpaceTopology
  { e with
    open_source := a.isOpen_source e,
    continuousOn_toFun := by
      refine continuousOn_iff'.mpr fun s hs => ‚ü®e ‚Åª¬π' s ‚à© e.source,
        isOpen_iSup_iff.mpr fun e' => ?_, by rw [inter_assoc, inter_self]; rfl‚ü©
      refine isOpen_iSup_iff.mpr fun he' => ?_
      rw [isOpen_coinduced, isOpen_induced_iff]
      obtain ‚ü®u, hu1, hu2‚ü© := continuousOn_iff'.mp (a.continuous_trivChange _ he _ he') s hs
      have hu3 := congr_arg (fun s => (fun x : e'.target => (x : B √ó F)) ‚Åª¬π' s) hu2
      simp only [Subtype.coe_preimage_self, preimage_inter, univ_inter] at hu3
      refine ‚ü®u ‚à© e'.toPartialEquiv.target ‚à© e'.toPartialEquiv.symm ‚Åª¬π' e.source, ?_, by
        simp only [preimage_inter, inter_univ, Subtype.coe_preimage_self, hu3.symm]; rfl‚ü©
      rw [inter_assoc]
      exact hu1.inter (a.isOpen_target_of_mem_pretrivializationAtlas_inter e e' he')
    continuousOn_invFun := a.continuous_symm_of_mem_pretrivializationAtlas he }
#align fiber_prebundle.trivialization_of_mem_pretrivialization_atlas FiberPrebundle.trivializationOfMemPretrivializationAtlas

theorem mem_pretrivializationAt_source (b : B) (x : E b) :
    ‚ü®b, x‚ü© ‚àà (a.pretrivializationAt b).source := by
  simp only [(a.pretrivializationAt b).source_eq, mem_preimage, TotalSpace.proj]
  exact a.mem_base_pretrivializationAt b
#align fiber_prebundle.mem_trivialization_at_source FiberPrebundle.mem_pretrivializationAt_source

@[simp]
theorem totalSpaceMk_preimage_source (b : B) :
    TotalSpace.mk b ‚Åª¬π' (a.pretrivializationAt b).source = univ :=
  eq_univ_of_forall (a.mem_pretrivializationAt_source b)
#align fiber_prebundle.total_space_mk_preimage_source FiberPrebundle.totalSpaceMk_preimage_source

@[continuity]
theorem continuous_totalSpaceMk (b : B) :
    Continuous[_, a.totalSpaceTopology] (TotalSpace.mk b) := by
  letI := a.totalSpaceTopology
  let e := a.trivializationOfMemPretrivializationAtlas (a.pretrivialization_mem_atlas b)
  rw [e.toPartialHomeomorph.continuous_iff_continuous_comp_left
      (a.totalSpaceMk_preimage_source b)]
  exact continuous_iff_le_induced.mpr (le_antisymm_iff.mp (a.totalSpaceMk_inducing b).induced).1
#align fiber_prebundle.continuous_total_space_mk FiberPrebundle.continuous_totalSpaceMk

theorem inducing_totalSpaceMk_of_inducing_comp (b : B)
    (h : Inducing (a.pretrivializationAt b ‚àò TotalSpace.mk b)) :
    @Inducing _ _ _ a.totalSpaceTopology (TotalSpace.mk b) := by
  letI := a.totalSpaceTopology
  rw [‚Üê restrict_comp_codRestrict (a.mem_pretrivializationAt_source b)] at h
  apply Inducing.of_codRestrict (a.mem_pretrivializationAt_source b)
  refine inducing_of_inducing_compose ?_ (continuousOn_iff_continuous_restrict.mp
    (a.trivializationOfMemPretrivializationAtlas
      (a.pretrivialization_mem_atlas b)).continuousOn_toFun) h
  exact (a.continuous_totalSpaceMk b).codRestrict (a.mem_pretrivializationAt_source b)
#align fiber_prebundle.inducing_total_space_mk_of_inducing_comp FiberPrebundle.inducing_totalSpaceMk_of_inducing_comp

/-- Make a `FiberBundle` from a `FiberPrebundle`.  Concretely this means
that, given a `FiberPrebundle` structure for a sigma-type `E` -- which consists of a
number of "pretrivializations" identifying parts of `E` with product spaces `U √ó F` -- one
establishes that for the topology constructed on the sigma-type using
`FiberPrebundle.totalSpaceTopology`, these "pretrivializations" are actually
"trivializations" (i.e., homeomorphisms with respect to the constructed topology). -/
def toFiberBundle : @FiberBundle B F _ _ E a.totalSpaceTopology _ :=
  let _ := a.totalSpaceTopology
  { totalSpaceMk_inducing' := fun b ‚Ü¶ a.inducing_totalSpaceMk_of_inducing_comp b
      (a.totalSpaceMk_inducing b)
    trivializationAtlas' :=
      { e | ‚àÉ (e‚ÇÄ : _) (he‚ÇÄ : e‚ÇÄ ‚àà a.pretrivializationAtlas),
        e = a.trivializationOfMemPretrivializationAtlas he‚ÇÄ },
    trivializationAt' := fun x ‚Ü¶
      a.trivializationOfMemPretrivializationAtlas (a.pretrivialization_mem_atlas x),
    mem_baseSet_trivializationAt' := a.mem_base_pretrivializationAt
    trivialization_mem_atlas' := fun x ‚Ü¶ ‚ü®_, a.pretrivialization_mem_atlas x, rfl‚ü© }
#align fiber_prebundle.to_fiber_bundle FiberPrebundle.toFiberBundle

theorem continuous_proj : @Continuous _ _ a.totalSpaceTopology _ (œÄ F E) := by
  letI := a.totalSpaceTopology
  letI := a.toFiberBundle
  exact FiberBundle.continuous_proj F E
#align fiber_prebundle.continuous_proj FiberPrebundle.continuous_proj

instance {e‚ÇÄ} (he‚ÇÄ : e‚ÇÄ ‚àà a.pretrivializationAtlas) :
    (letI := a.totalSpaceTopology; letI := a.toFiberBundle;
      MemTrivializationAtlas (a.trivializationOfMemPretrivializationAtlas he‚ÇÄ)) :=
  letI := a.totalSpaceTopology; letI := a.toFiberBundle; ‚ü®e‚ÇÄ, he‚ÇÄ, rfl‚ü©

/-- For a fiber bundle `E` over `B` constructed using the `FiberPrebundle` mechanism,
continuity of a function `TotalSpace F E ‚Üí X` on an open set `s` can be checked by precomposing at
each point with the pretrivialization used for the construction at that point. -/
theorem continuousOn_of_comp_right {X : Type*} [TopologicalSpace X] {f : TotalSpace F E ‚Üí X}
    {s : Set B} (hs : IsOpen s) (hf : ‚àÄ b ‚àà s,
      ContinuousOn (f ‚àò (a.pretrivializationAt b).toPartialEquiv.symm)
        ((s ‚à© (a.pretrivializationAt b).baseSet) √óÀ¢ (Set.univ : Set F))) :
    @ContinuousOn _ _ a.totalSpaceTopology _ f (œÄ F E ‚Åª¬π' s) := by
  letI := a.totalSpaceTopology
  intro z hz
  let e : Trivialization F (œÄ F E) :=
    a.trivializationOfMemPretrivializationAtlas (a.pretrivialization_mem_atlas z.proj)
  refine (e.continuousAt_of_comp_right ?_
    ((hf z.proj hz).continuousAt (IsOpen.mem_nhds ?_ ?_))).continuousWithinAt
  ¬∑ exact a.mem_base_pretrivializationAt z.proj
  ¬∑ exact (hs.inter (a.pretrivializationAt z.proj).open_baseSet).prod isOpen_univ
  refine ‚ü®?_, mem_univ _‚ü©
  rw [e.coe_fst]
  ¬∑ exact ‚ü®hz, a.mem_base_pretrivializationAt z.proj‚ü©
  ¬∑ rw [e.mem_source]
    exact a.mem_base_pretrivializationAt z.proj
#align fiber_prebundle.continuous_on_of_comp_right FiberPrebundle.continuousOn_of_comp_right

end FiberPrebundle
