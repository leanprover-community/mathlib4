/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Topology.Metrizable.Basic
import Mathlib.Data.Nat.Lattice

#align_import topology.metric_space.metrizable_uniformity from "leanprover-community/mathlib"@"195fcd60ff2bfe392543bceb0ec2adcdb472db4c"

/-!
# Metrizable uniform spaces

In this file we prove that a uniform space with countably generated uniformity filter is
pseudometrizable: there exists a `PseudoMetricSpace` structure that generates the same uniformity.
The proof follows [Sergey Melikhov, Metrizable uniform spaces][melikhov2011].

## Main definitions

* `PseudoMetricSpace.ofPreNNDist`: given a function `d : X ‚Üí X ‚Üí ‚Ñù‚â•0` such that `d x x = 0` and
  `d x y = d y x` for all `x y : X`, constructs the maximal pseudo metric space structure such that
  `NNDist x y ‚â§ d x y` for all `x y : X`.

* `UniformSpace.pseudoMetricSpace`: given a uniform space `X` with countably generated `ùì§ X`,
  constructs a `PseudoMetricSpace X` instance that is compatible with the uniform space structure.

* `UniformSpace.metricSpace`: given a T‚ÇÄ uniform space `X` with countably generated `ùì§ X`,
  constructs a `MetricSpace X` instance that is compatible with the uniform space structure.

## Main statements

* `UniformSpace.metrizable_uniformity`: if `X` is a uniform space with countably generated `ùì§ X`,
  then there exists a `PseudoMetricSpace` structure that is compatible with this `UniformSpace`
  structure. Use `UniformSpace.pseudoMetricSpace` or `UniformSpace.metricSpace` instead.

* `UniformSpace.pseudoMetrizableSpace`: a uniform space with countably generated `ùì§ X` is pseudo
  metrizable.

* `UniformSpace.metrizableSpace`: a T‚ÇÄ uniform space with countably generated `ùì§ X` is
  metrizable. This is not an instance to avoid loops.

## Tags

metrizable space, uniform space
-/


open Set Function Metric List Filter

open NNReal Filter Uniformity

variable {X : Type*}

namespace PseudoMetricSpace

/-- The maximal pseudo metric space structure on `X` such that `dist x y ‚â§ d x y` for all `x y`,
where `d : X ‚Üí X ‚Üí ‚Ñù‚â•0` is a function such that `d x x = 0` and `d x y = d y x` for all `x`, `y`. -/
noncomputable def ofPreNNDist (d : X ‚Üí X ‚Üí ‚Ñù‚â•0) (dist_self : ‚àÄ x, d x x = 0)
    (dist_comm : ‚àÄ x y, d x y = d y x) : PseudoMetricSpace X where
  dist x y := ‚Üë(‚®Ö l : List X, ((x::l).zipWith d (l ++ [y])).sum : ‚Ñù‚â•0)
  dist_self x := NNReal.coe_eq_zero.2 <|
      nonpos_iff_eq_zero.1 <| (ciInf_le (OrderBot.bddBelow _) []).trans_eq <| by simp [dist_self]
  dist_comm x y :=
    NNReal.coe_inj.2 <| by
      refine' reverse_surjective.iInf_congr _ fun l => _
      rw [‚Üê sum_reverse, zipWith_distrib_reverse, reverse_append, reverse_reverse,
        reverse_singleton, singleton_append, reverse_cons, reverse_reverse,
        zipWith_comm_of_comm _ dist_comm]
      simp only [length, length_append]
  dist_triangle x y z := by
    -- Porting note: added `unfold`
    unfold dist
    rw [‚Üê NNReal.coe_add, NNReal.coe_le_coe]
    refine' NNReal.le_iInf_add_iInf fun lxy lyz => _
    calc
      ‚®Ö l, (zipWith d (x::l) (l ++ [z])).sum ‚â§
          (zipWith d (x::lxy ++ y::lyz) ((lxy ++ y::lyz) ++ [z])).sum :=
        ciInf_le (OrderBot.bddBelow _) (lxy ++ y::lyz)
      _ = (zipWith d (x::lxy) (lxy ++ [y])).sum + (zipWith d (y::lyz) (lyz ++ [z])).sum := by
        rw [‚Üê sum_append, ‚Üê zipWith_append, cons_append, ‚Üê @singleton_append _ y, append_assoc,
          append_assoc, append_assoc]
        rw [length_cons, length_append, length_singleton]
  -- Porting note: `edist_dist` is no longer inferred
  edist_dist x y := rfl
#align pseudo_metric_space.of_prenndist PseudoMetricSpace.ofPreNNDist

theorem dist_ofPreNNDist (d : X ‚Üí X ‚Üí ‚Ñù‚â•0) (dist_self : ‚àÄ x, d x x = 0)
    (dist_comm : ‚àÄ x y, d x y = d y x) (x y : X) :
    @dist X (@PseudoMetricSpace.toDist X (PseudoMetricSpace.ofPreNNDist d dist_self dist_comm)) x
        y =
      ‚Üë(‚®Ö l : List X, ((x::l).zipWith d (l ++ [y])).sum : ‚Ñù‚â•0) :=
  rfl
#align pseudo_metric_space.dist_of_prenndist PseudoMetricSpace.dist_ofPreNNDist

theorem dist_ofPreNNDist_le (d : X ‚Üí X ‚Üí ‚Ñù‚â•0) (dist_self : ‚àÄ x, d x x = 0)
    (dist_comm : ‚àÄ x y, d x y = d y x) (x y : X) :
    @dist X (@PseudoMetricSpace.toDist X (PseudoMetricSpace.ofPreNNDist d dist_self dist_comm)) x
        y ‚â§
      d x y :=
  NNReal.coe_le_coe.2 <| (ciInf_le (OrderBot.bddBelow _) []).trans_eq <| by simp
#align pseudo_metric_space.dist_of_prenndist_le PseudoMetricSpace.dist_ofPreNNDist_le

/-- Consider a function `d : X ‚Üí X ‚Üí ‚Ñù‚â•0` such that `d x x = 0` and `d x y = d y x` for all `x`,
`y`. Let `dist` be the largest pseudometric distance such that `dist x y ‚â§ d x y`, see
`PseudoMetricSpace.ofPreNNDist`. Suppose that `d` satisfies the following triangle-like
inequality: `d x‚ÇÅ x‚ÇÑ ‚â§ 2 * max (d x‚ÇÅ x‚ÇÇ, d x‚ÇÇ x‚ÇÉ, d x‚ÇÉ x‚ÇÑ)`. Then `d x y ‚â§ 2 * dist x y` for all
`x`, `y`. -/
theorem le_two_mul_dist_ofPreNNDist (d : X ‚Üí X ‚Üí ‚Ñù‚â•0) (dist_self : ‚àÄ x, d x x = 0)
    (dist_comm : ‚àÄ x y, d x y = d y x)
    (hd : ‚àÄ x‚ÇÅ x‚ÇÇ x‚ÇÉ x‚ÇÑ, d x‚ÇÅ x‚ÇÑ ‚â§ 2 * max (d x‚ÇÅ x‚ÇÇ) (max (d x‚ÇÇ x‚ÇÉ) (d x‚ÇÉ x‚ÇÑ))) (x y : X) :
    ‚Üë(d x y) ‚â§ 2 * @dist X
      (@PseudoMetricSpace.toDist X (PseudoMetricSpace.ofPreNNDist d dist_self dist_comm)) x y := by
  /- We need to show that `d x y` is at most twice the sum `L` of `d x·µ¢ x·µ¢‚Çä‚ÇÅ` over a path
    `x‚ÇÄ=x, ..., x‚Çô=y`. We prove it by induction on the length `n` of the sequence. Find an edge that
    splits the path into two parts of almost equal length: both `d x‚ÇÄ x‚ÇÅ + ... + d x‚Çñ‚Çã‚ÇÅ x‚Çñ` and
    `d x‚Çñ‚Çä‚ÇÅ x‚Çñ‚Çä‚ÇÇ + ... + d x‚Çô‚Çã‚ÇÅ x‚Çô` are less than or equal to `L / 2`.
    Then `d x‚ÇÄ x‚Çñ ‚â§ L`, `d x‚Çñ x‚Çñ‚Çä‚ÇÅ ‚â§ L`, and `d x‚Çñ‚Çä‚ÇÅ x‚Çô ‚â§ L`, thus `d x‚ÇÄ x‚Çô ‚â§ 2 * L`. -/
  rw [dist_ofPreNNDist, ‚Üê NNReal.coe_two, ‚Üê NNReal.coe_mul, NNReal.mul_iInf, NNReal.coe_le_coe]
  refine' le_ciInf fun l => _
  have hd‚ÇÄ_trans : Transitive fun x y => d x y = 0 := by
    intro a b c hab hbc
    rw [‚Üê nonpos_iff_eq_zero]
    simpa only [nonpos_iff_eq_zero, hab, hbc, dist_self c, max_self, mul_zero] using hd a b c c
  haveI : IsTrans X fun x y => d x y = 0 := ‚ü®hd‚ÇÄ_trans‚ü©
  induction' hn : length l using Nat.strong_induction_on with n ihn generalizing x y l
  simp only at ihn
  subst n
  set L := zipWith d (x::l) (l ++ [y])
  have hL_len : length L = length l + 1 := by simp [L]
  rcases eq_or_ne (d x y) 0 with hd‚ÇÄ | hd‚ÇÄ
  ¬∑ simp only [hd‚ÇÄ, zero_le]
  rsuffices ‚ü®z, z', hxz, hzz', hz'y‚ü© : ‚àÉ z z' : X, d x z ‚â§ L.sum ‚àß d z z' ‚â§ L.sum ‚àß d z' y ‚â§ L.sum
  ¬∑ exact (hd x z z' y).trans (mul_le_mul_left' (max_le hxz (max_le hzz' hz'y)) _)
  set s : Set ‚Ñï := { m : ‚Ñï | 2 * (take m L).sum ‚â§ L.sum }
  have hs‚ÇÄ : 0 ‚àà s := by simp [s]
  have hsne : s.Nonempty := ‚ü®0, hs‚ÇÄ‚ü©
  obtain ‚ü®M, hMl, hMs‚ü© : ‚àÉ M ‚â§ length l, IsGreatest s M := by
    have hs_ub : length l ‚àà upperBounds s := by
      intro m hm
      rw [‚Üê not_lt, Nat.lt_iff_add_one_le, ‚Üê hL_len]
      intro hLm
      rw [mem_setOf_eq, take_all_of_le hLm, two_mul, add_le_iff_nonpos_left, nonpos_iff_eq_zero,
          sum_eq_zero_iff, ‚Üê forall_iff_forall_mem, forall_zipWith,
          ‚Üê chain_append_singleton_iff_forall‚ÇÇ]
          at hm <;>
        [skip; simp]
      exact hd‚ÇÄ (hm.rel (mem_append.2 <| Or.inr <| mem_singleton_self _))
    have hs_bdd : BddAbove s := ‚ü®length l, hs_ub‚ü©
    exact ‚ü®sSup s, csSup_le hsne hs_ub, ‚ü®Nat.sSup_mem hsne hs_bdd, fun k => le_csSup hs_bdd‚ü©‚ü©
  have hM_lt : M < length L := by rwa [hL_len, Nat.lt_succ_iff]
  have hM_ltx : M < length (x::l) := lt_length_left_of_zipWith hM_lt
  have hM_lty : M < length (l ++ [y]) := lt_length_right_of_zipWith hM_lt
  refine' ‚ü®(x::l).get ‚ü®M, hM_ltx‚ü©, (l ++ [y]).get ‚ü®M, hM_lty‚ü©, _, _, _‚ü©
  ¬∑ cases M with
    | zero =>
      simp [dist_self, List.get]
    | succ M =>
      rw [Nat.succ_le_iff] at hMl
      have hMl' : length (take M l) = M := (length_take _ _).trans (min_eq_left hMl.le)
      simp only [List.get]
      refine' (ihn _ hMl _ _ _ hMl').trans _
      convert hMs.1.out
      rw [zipWith_distrib_take, take, take_succ, get?_append hMl, get?_eq_get hMl, ‚Üê Option.coe_def,
        Option.toList_some, take_append_of_le_length hMl.le]
  ¬∑ exact single_le_sum (fun x _ => zero_le x) _ (mem_iff_get.2 ‚ü®‚ü®M, hM_lt‚ü©, get_zipWith‚ü©)
  ¬∑ rcases hMl.eq_or_lt with (rfl | hMl)
    ¬∑ simp only [get_append_right' le_rfl, sub_self, get_singleton, dist_self, zero_le]
    rw [get_append _ hMl]
    have hlen : length (drop (M + 1) l) = length l - (M + 1) := length_drop _ _
    have hlen_lt : length l - (M + 1) < length l := Nat.sub_lt_of_pos_le M.succ_pos hMl
    refine' (ihn _ hlen_lt _ y _ hlen).trans _
    rw [cons_get_drop_succ]
    have hMs' : L.sum ‚â§ 2 * (L.take (M + 1)).sum :=
      not_lt.1 fun h => (hMs.2 h.le).not_lt M.lt_succ_self
    rw [‚Üê sum_take_add_sum_drop L (M + 1), two_mul, add_le_add_iff_left, ‚Üê add_le_add_iff_right,
      sum_take_add_sum_drop, ‚Üê two_mul] at hMs'
    convert hMs'
    rwa [zipWith_distrib_drop, drop, drop_append_of_le_length]
#align pseudo_metric_space.le_two_mul_dist_of_prenndist PseudoMetricSpace.le_two_mul_dist_ofPreNNDist

end PseudoMetricSpace

-- Porting note (#11083): this is slower than in Lean3 for some reason...
/-- If `X` is a uniform space with countably generated uniformity filter, there exists a
`PseudoMetricSpace` structure compatible with the `UniformSpace` structure. Use
`UniformSpace.pseudoMetricSpace` or `UniformSpace.metricSpace` instead. -/
protected theorem UniformSpace.metrizable_uniformity (X : Type*) [UniformSpace X]
    [IsCountablyGenerated (ùì§ X)] : ‚àÉ I : PseudoMetricSpace X, I.toUniformSpace = ‚Äπ_‚Ä∫ := by
  classical
  /- Choose a fast decreasing antitone basis `U : ‚Ñï ‚Üí set (X √ó X)` of the uniformity filter `ùì§ X`.
    Define `d x y : ‚Ñù‚â•0` to be `(1 / 2) ^ n`, where `n` is the minimal index of `U n` that
    separates `x` and `y`: `(x, y) ‚àâ U n`, or `0` if `x` is not separated from `y`. This function
    satisfies the assumptions of `PseudoMetricSpace.ofPreNNDist` and
    `PseudoMetricSpace.le_two_mul_dist_ofPreNNDist`, hence the distance given by the former pseudo
    metric space structure is Lipschitz equivalent to the `d`. Thus the uniformities generated by
    `d` and `dist` are equal. Since the former uniformity is equal to `ùì§ X`, the latter is equal to
    `ùì§ X` as well. -/
  obtain ‚ü®U, hU_symm, hU_comp, hB‚ü© :
    ‚àÉ U : ‚Ñï ‚Üí Set (X √ó X),
      (‚àÄ n, SymmetricRel (U n)) ‚àß
        (‚àÄ ‚¶Ém n‚¶Ñ, m < n ‚Üí U n ‚óã (U n ‚óã U n) ‚äÜ U m) ‚àß (ùì§ X).HasAntitoneBasis U := by
    rcases UniformSpace.has_seq_basis X with ‚ü®V, hB, hV_symm‚ü©
    rcases hB.subbasis_with_rel fun m =>
        hB.tendsto_smallSets.eventually
          (eventually_uniformity_iterate_comp_subset (hB.mem m) 2) with
      ‚ü®œÜ, -, hœÜ_comp, hœÜB‚ü©
    exact ‚ü®V ‚àò œÜ, fun n => hV_symm _, hœÜ_comp, hœÜB‚ü©
  set d : X ‚Üí X ‚Üí ‚Ñù‚â•0 := fun x y => if h : ‚àÉ n, (x, y) ‚àâ U n then (1 / 2) ^ Nat.find h else 0
  have hd‚ÇÄ : ‚àÄ {x y}, d x y = 0 ‚Üî Inseparable x y := by
    intro x y
    refine' Iff.trans _ hB.inseparable_iff_uniformity.symm
    simp only [d, true_imp_iff]
    split_ifs with h
    ¬∑ rw [‚Üê not_forall] at h
      simp [h, pow_eq_zero_iff']
    ¬∑ simpa only [not_exists, Classical.not_not, eq_self_iff_true, true_iff_iff] using h
  have hd_symm : ‚àÄ x y, d x y = d y x := by
    intro x y
    simp only [d, @SymmetricRel.mk_mem_comm _ _ (hU_symm _) x y]
  have hr : (1 / 2 : ‚Ñù‚â•0) ‚àà Ioo (0 : ‚Ñù‚â•0) 1 := ‚ü®half_pos one_pos, NNReal.half_lt_self one_ne_zero‚ü©
  letI I := PseudoMetricSpace.ofPreNNDist d (fun x => hd‚ÇÄ.2 rfl) hd_symm
  have hdist_le : ‚àÄ x y, dist x y ‚â§ d x y := PseudoMetricSpace.dist_ofPreNNDist_le _ _ _
  have hle_d : ‚àÄ {x y : X} {n : ‚Ñï}, (1 / 2) ^ n ‚â§ d x y ‚Üî (x, y) ‚àâ U n := by
    intro x y n
    dsimp only [d]
    split_ifs with h
    ¬∑ rw [(pow_right_strictAnti hr.1 hr.2).le_iff_le, Nat.find_le_iff]
      exact ‚ü®fun ‚ü®m, hmn, hm‚ü© hn => hm (hB.antitone hmn hn), fun h => ‚ü®n, le_rfl, h‚ü©‚ü©
    ¬∑ push_neg at h
      simp only [h, not_true, (pow_pos hr.1 _).not_le]
  have hd_le : ‚àÄ x y, ‚Üë(d x y) ‚â§ 2 * dist x y := by
    refine' PseudoMetricSpace.le_two_mul_dist_ofPreNNDist _ _ _ fun x‚ÇÅ x‚ÇÇ x‚ÇÉ x‚ÇÑ => _
    by_cases H : ‚àÉ n, (x‚ÇÅ, x‚ÇÑ) ‚àâ U n
    ¬∑ refine' (dif_pos H).trans_le _
      rw [‚Üê NNReal.div_le_iff' two_ne_zero, ‚Üê mul_one_div (_ ^ _), ‚Üê pow_succ]
      simp only [le_max_iff, hle_d, ‚Üê not_and_or]
      rintro ‚ü®h‚ÇÅ‚ÇÇ, h‚ÇÇ‚ÇÉ, h‚ÇÉ‚ÇÑ‚ü©
      refine' Nat.find_spec H (hU_comp (lt_add_one <| Nat.find H) _)
      exact ‚ü®x‚ÇÇ, h‚ÇÅ‚ÇÇ, x‚ÇÉ, h‚ÇÇ‚ÇÉ, h‚ÇÉ‚ÇÑ‚ü©
    ¬∑ exact (dif_neg H).trans_le (zero_le _)
  -- Porting note: without the next line, `uniformity_basis_dist_pow` ends up introducing some
  -- `Subtype.val` applications instead of `NNReal.toReal`.
  rw [mem_Ioo, ‚Üê NNReal.coe_lt_coe, ‚Üê NNReal.coe_lt_coe] at hr
  refine' ‚ü®I, UniformSpace.ext <| (uniformity_basis_dist_pow hr.1 hr.2).ext hB.toHasBasis _ _‚ü©
  ¬∑ refine' fun n hn => ‚ü®n, hn, fun x hx => (hdist_le _ _).trans_lt _‚ü©
    rwa [‚Üê NNReal.coe_pow, NNReal.coe_lt_coe, ‚Üê not_le, hle_d, Classical.not_not]
  ¬∑ refine' fun n _ => ‚ü®n + 1, trivial, fun x hx => _‚ü©
    rw [mem_setOf_eq] at hx
    contrapose! hx
    refine' le_trans _ ((div_le_iff' (zero_lt_two' ‚Ñù)).2 (hd_le x.1 x.2))
    rwa [‚Üê NNReal.coe_two, ‚Üê NNReal.coe_div, ‚Üê NNReal.coe_pow, NNReal.coe_le_coe, pow_succ,
      mul_one_div, NNReal.div_le_iff two_ne_zero, div_mul_cancel‚ÇÄ _ (two_ne_zero' ‚Ñù‚â•0), hle_d]
#align uniform_space.metrizable_uniformity UniformSpace.metrizable_uniformity

/-- A `PseudoMetricSpace` instance compatible with a given `UniformSpace` structure. -/
protected noncomputable def UniformSpace.pseudoMetricSpace (X : Type*) [UniformSpace X]
    [IsCountablyGenerated (ùì§ X)] : PseudoMetricSpace X :=
  (UniformSpace.metrizable_uniformity X).choose.replaceUniformity <|
    congr_arg _ (UniformSpace.metrizable_uniformity X).choose_spec.symm
#align uniform_space.pseudo_metric_space UniformSpace.pseudoMetricSpace

/-- A `MetricSpace` instance compatible with a given `UniformSpace` structure. -/
protected noncomputable def UniformSpace.metricSpace (X : Type*) [UniformSpace X]
    [IsCountablyGenerated (ùì§ X)] [T0Space X] : MetricSpace X :=
  @MetricSpace.ofT0PseudoMetricSpace X (UniformSpace.pseudoMetricSpace X) _
#align uniform_space.metric_space UniformSpace.metricSpace

/-- A uniform space with countably generated `ùì§ X` is pseudo metrizable. -/
instance (priority := 100) UniformSpace.pseudoMetrizableSpace [UniformSpace X]
    [IsCountablyGenerated (ùì§ X)] : TopologicalSpace.PseudoMetrizableSpace X := by
  letI := UniformSpace.pseudoMetricSpace X
  infer_instance
#align uniform_space.pseudo_metrizable_space UniformSpace.pseudoMetrizableSpace

/-- A T‚ÇÄ uniform space with countably generated `ùì§ X` is metrizable. This is not an instance to
avoid loops. -/
theorem UniformSpace.metrizableSpace [UniformSpace X] [IsCountablyGenerated (ùì§ X)] [T0Space X] :
    TopologicalSpace.MetrizableSpace X := by
  letI := UniformSpace.metricSpace X
  infer_instance
#align uniform_space.metrizable_space UniformSpace.metrizableSpace
