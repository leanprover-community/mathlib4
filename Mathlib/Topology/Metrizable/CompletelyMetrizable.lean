/-
Copyright (c) 2025 Etienne Marion. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Etienne Marion
-/
import Mathlib.Topology.MetricSpace.Gluing
import Mathlib.Topology.Metrizable.Uniformity

/-!
# Completely metrizable spaces

A topological space is completely metrizable if one can endow it mith a `MetricSpace` structure
which makes it complete and gives the same topology. This typeclass allows to state theorems
which do not require a `MetricSpace` structure to make sense without introducing such a structure.
It is in particular useful in measure theory, where one often assumes that a space is a
`PolishSpace`, i.e. a separable and completely metrizable space. Sometimes the separability
hypothesis is not needed and the right assumption is then `IsCompletelyMetrizableSpace`.

## Main definition

* `IsCompletelyMetrizableSpace X`: A topological space is completely metrizable if there exists a
  metric space structure compatible with the topology which makes the space complete.
  To endow such a space with a compatible distance, use `letI := upgradeIsCompletelyMetrizable X`.

## Implementation note

Given a `IsCompletelyMetrizableSpace X` instance, one may want to endow `X` with a complete metric.
This can be done by writing `letI := upgradeIsCompletelyMetrizable X`, which will endow `X` with
an `UpgradedIsCompletelyMetrizableSpace X` instance. This class is a convenience class and
no instance should be registered for it.
-/

open Filter Function Set Topology

variable {X Y : Type*}

namespace TopologicalSpace

/-- A topological space is completely metrizable if there exists a metric space structure
compatible with the topology which makes the space complete.
To endow such a space with a compatible distance, use
`letI := upgradeIsCompletelyMetrizable X`. -/
class IsCompletelyMetrizableSpace (X : Type*) [t : TopologicalSpace X] : Prop where
  complete : ‚àÉ m : MetricSpace X, m.toUniformSpace.toTopologicalSpace = t ‚àß
    @CompleteSpace X m.toUniformSpace

instance (priority := 100) _root_.MetricSpace.toIsCompletelyMetrizableSpace
    [MetricSpace X] [CompleteSpace X] : IsCompletelyMetrizableSpace X :=
  ‚ü®‚ü®‚Äπ_‚Ä∫, rfl, ‚Äπ_‚Ä∫‚ü©‚ü©

/-- A convenience class, for a completely metrizable space endowed with a complete metric.
No instance of this class should be registered: It should be used as
`letI := upgradeIsCompletelyMetrizable X` to endow a completely metrizable
space with a complete metric. -/
class UpgradedIsCompletelyMetrizableSpace (X : Type*) extends MetricSpace X, CompleteSpace X

open scoped Uniformity in
instance (priority := 100) IsCompletelyMetrizableSpace.of_completeSpace_metrizable [UniformSpace X]
    [CompleteSpace X] [(ùì§ X).IsCountablyGenerated] [T0Space X] :
    IsCompletelyMetrizableSpace X where
  complete := ‚ü®UniformSpace.metricSpace X, rfl, ‚Äπ_‚Ä∫‚ü©

/-- Construct on a completely metrizable space a metric (compatible with the topology)
which is complete. -/
noncomputable def completelyMetrizableMetric (X : Type*) [TopologicalSpace X]
    [h : IsCompletelyMetrizableSpace X] : MetricSpace X :=
  h.complete.choose.replaceTopology h.complete.choose_spec.1.symm

theorem complete_completelyMetrizableMetric (X : Type*) [ht : TopologicalSpace X]
    [h : IsCompletelyMetrizableSpace X] :
    @CompleteSpace X (completelyMetrizableMetric X).toUniformSpace := by
  convert h.complete.choose_spec.2
  exact MetricSpace.replaceTopology_eq _ _

/-- This definition endows a completely metrizable space with a complete metric. Use it as:
`letI := upgradeIsCompletelyMetrizable X`. -/
noncomputable
def upgradeIsCompletelyMetrizable (X : Type*) [TopologicalSpace X] [IsCompletelyMetrizableSpace X] :
    UpgradedIsCompletelyMetrizableSpace X :=
  letI := completelyMetrizableMetric X
  { complete_completelyMetrizableMetric X with }

namespace IsCompletelyMetrizableSpace

/-- Note: the priority is set to 90 to ensure that this instance is only applied after
`EMetricSpace.metrizableSpace`. This prevents unnecessary attempts to infer completeness. -/
instance (priority := 90) MetrizableSpace [TopologicalSpace X] [IsCompletelyMetrizableSpace X] :
    MetrizableSpace X := by
  letI := upgradeIsCompletelyMetrizable X
  infer_instance

/-- A countable product of completely metrizable spaces is completely metrizable. -/
instance pi_countable {Œπ : Type*} [Countable Œπ] {X : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (X i)]
    [‚àÄ i, IsCompletelyMetrizableSpace (X i)] : IsCompletelyMetrizableSpace (Œ† i, X i) := by
  letI := fun i ‚Ü¶ upgradeIsCompletelyMetrizable (X i)
  infer_instance

/-- A disjoint union of completely metrizable spaces is completely metrizable. -/
instance sigma {Œπ : Type*} {X : Œπ ‚Üí Type*} [‚àÄ n, TopologicalSpace (X n)]
    [‚àÄ n, IsCompletelyMetrizableSpace (X n)] : IsCompletelyMetrizableSpace (Œ£ n, X n) :=
  letI := fun n ‚Ü¶ upgradeIsCompletelyMetrizable (X n)
  letI : MetricSpace (Œ£ n, X n) := Metric.Sigma.metricSpace
  haveI : CompleteSpace (Œ£ n, X n) := Metric.Sigma.completeSpace
  inferInstance

/-- The product of two completely metrizable spaces is completely metrizable. -/
instance prod [TopologicalSpace X] [IsCompletelyMetrizableSpace X] [TopologicalSpace Y]
    [IsCompletelyMetrizableSpace Y] : IsCompletelyMetrizableSpace (X √ó Y) :=
  letI := upgradeIsCompletelyMetrizable X
  letI := upgradeIsCompletelyMetrizable Y
  inferInstance

/-- The disjoint union of two completely metrizable spaces is completely metrizable. -/
instance sum [TopologicalSpace X] [IsCompletelyMetrizableSpace X] [TopologicalSpace Y]
    [IsCompletelyMetrizableSpace Y] : IsCompletelyMetrizableSpace (X ‚äï Y) :=
  letI := upgradeIsCompletelyMetrizable X
  letI := upgradeIsCompletelyMetrizable Y
  inferInstance

/-- Given a closed embedding into a completely metrizable space,
the source space is also completely metrizable. -/
theorem _root_.Topology.IsClosedEmbedding.IsCompletelyMetrizableSpace [TopologicalSpace X]
    [TopologicalSpace Y] [IsCompletelyMetrizableSpace Y] {f : X ‚Üí Y} (hf : IsClosedEmbedding f) :
    IsCompletelyMetrizableSpace X := by
  letI := upgradeIsCompletelyMetrizable Y
  letI : MetricSpace X := hf.isEmbedding.comapMetricSpace f
  have : CompleteSpace X := by
    rw [completeSpace_iff_isComplete_range hf.isEmbedding.to_isometry.isUniformInducing]
    exact hf.isClosed_range.isComplete
  infer_instance

/-- Any discrete space is completely metrizable. -/
instance (priority := 50) discrete [TopologicalSpace X] [DiscreteTopology X] :
    IsCompletelyMetrizableSpace X := by
  classical
  let m : MetricSpace X :=
    { dist x y := if x = y then 0 else 1
      dist_self x := by simp
      dist_comm x y := by
        obtain h | h := eq_or_ne x y
        ¬∑ simp [h]
        ¬∑ simp [h, h.symm]
      dist_triangle x y z := by
        by_cases x = y <;> by_cases x = z <;> by_cases y = z <;> simp_all
      eq_of_dist_eq_zero := by simp }
  refine ‚ü®m, ?_, ?_‚ü©
  ¬∑ rw [DiscreteTopology.eq_bot (Œ± := X)]
    refine eq_bot_of_singletons_open fun x ‚Ü¶ ?_
    convert @Metric.isOpen_ball _ _ x 1
    refine subset_antisymm (singleton_subset_iff.2 (Metric.mem_ball_self (by simp)))
      fun y hy ‚Ü¶ ?_
    simp only [Metric.mem_ball, mem_singleton_iff] at *
    by_contra
    change (if y = x then 0 else 1) < 1 at hy
    simp_all
  ¬∑ refine Metric.complete_of_cauchySeq_tendsto fun u hu ‚Ü¶ ?_
    rw [Metric.cauchySeq_iff'] at hu
    obtain ‚ü®N, hN‚ü© := hu 1 (by simp)
    refine ‚ü®u N, @tendsto_atTop_of_eventually_const X UniformSpace.toTopologicalSpace (u N) _ _ _ N
      fun n hn ‚Ü¶ ?_‚ü©
    specialize hN n hn
    by_contra
    change (if u n = u N then 0 else 1) < 1 at hN
    simp_all

/-- A closed subset of a completely metrizable space is also completely metrizable. -/
theorem _root_.IsClosed.isCompletelyMetrizableSpace
    [TopologicalSpace X] [IsCompletelyMetrizableSpace X]
    {s : Set X} (hs : IsClosed s) : IsCompletelyMetrizableSpace s :=
  hs.isClosedEmbedding_subtypeVal.IsCompletelyMetrizableSpace

instance univ [TopologicalSpace X] [IsCompletelyMetrizableSpace X] :
    IsCompletelyMetrizableSpace (univ : Set X) :=
  isClosed_univ.isCompletelyMetrizableSpace

end IsCompletelyMetrizableSpace

end TopologicalSpace
