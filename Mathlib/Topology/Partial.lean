/-
Copyright (c) 2018 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad
-/
import Mathlib.Order.Filter.Partial
import Mathlib.Topology.Neighborhoods

/-!
# Partial functions and topological spaces

In this file we prove properties of `Filter.PTendsto` etc in topological spaces. We also introduce
`PContinuous`, a version of `Continuous` for partially defined functions.
-/


open Filter

open Topology

variable {X Y : Type*} [TopologicalSpace X]

theorem rtendsto_nhds {r : Rel Y X} {l : Filter Y} {x : X} :
    RTendsto r l (ğ“ x) â†” âˆ€ s, IsOpen s â†’ x âˆˆ s â†’ r.core s âˆˆ l :=
  all_mem_nhds_filter _ _ (fun _s _t => id) _

theorem rtendsto'_nhds {r : Rel Y X} {l : Filter Y} {x : X} :
    RTendsto' r l (ğ“ x) â†” âˆ€ s, IsOpen s â†’ x âˆˆ s â†’ r.preimage s âˆˆ l := by
  rw [rtendsto'_def]
  apply all_mem_nhds_filter
  apply Rel.preimage_mono

theorem ptendsto_nhds {f : Y â†’. X} {l : Filter Y} {x : X} :
    PTendsto f l (ğ“ x) â†” âˆ€ s, IsOpen s â†’ x âˆˆ s â†’ f.core s âˆˆ l :=
  rtendsto_nhds

theorem ptendsto'_nhds {f : Y â†’. X} {l : Filter Y} {x : X} :
    PTendsto' f l (ğ“ x) â†” âˆ€ s, IsOpen s â†’ x âˆˆ s â†’ f.preimage s âˆˆ l :=
  rtendsto'_nhds

/-! ### Continuity and partial functions -/


variable [TopologicalSpace Y]

/-- Continuity of a partial function -/
def PContinuous (f : X â†’. Y) :=
  âˆ€ s, IsOpen s â†’ IsOpen (f.preimage s)

theorem open_dom_of_pcontinuous {f : X â†’. Y} (h : PContinuous f) : IsOpen f.Dom := by
  rw [â† PFun.preimage_univ]; exact h _ isOpen_univ

theorem pcontinuous_iff' {f : X â†’. Y} :
    PContinuous f â†” âˆ€ {x y} (_ : y âˆˆ f x), PTendsto' f (ğ“ x) (ğ“ y) := by
  constructor
  Â· intro h x y h'
    simp only [ptendsto'_def, mem_nhds_iff]
    rintro s âŸ¨t, tsubs, opent, ytâŸ©
    exact âŸ¨f.preimage t, PFun.preimage_mono _ tsubs, h _ opent, âŸ¨y, yt, h'âŸ©âŸ©
  intro hf s os
  rw [isOpen_iff_nhds]
  rintro x âŸ¨y, ys, fxyâŸ© t
  rw [mem_principal]
  intro (h : f.preimage s âŠ† t)
  apply mem_of_superset _ h
  have h' : âˆ€ s âˆˆ ğ“ y, f.preimage s âˆˆ ğ“ x := by
    intro s hs
    have : PTendsto' f (ğ“ x) (ğ“ y) := hf fxy
    rw [ptendsto'_def] at this
    exact this s hs
  show f.preimage s âˆˆ ğ“ x
  apply h'
  rw [mem_nhds_iff]
  exact âŸ¨s, Set.Subset.refl _, os, ysâŸ©

theorem continuousWithinAt_iff_ptendsto_res (f : X â†’ Y) {x : X} {s : Set X} :
    ContinuousWithinAt f s x â†” PTendsto (PFun.res f s) (ğ“ x) (ğ“ (f x)) :=
  tendsto_iff_ptendsto _ _ _ _
