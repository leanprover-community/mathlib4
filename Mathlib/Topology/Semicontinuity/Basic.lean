import Mathlib.Order.OmegaCompletePartialOrder
import Mathlib.Order.Filter.Extr
import Mathlib.Topology.Order.Real
import Mathlib.Topology.Algebra.InfiniteSum.Defs
import Mathlib.Topology.Algebra.Monoid.Defs

/-!
# Lower and Upper Semicontinuity

This file develops key properties of upper and lower semicontinuous functions.

## Main definitions and results

We have some equivalent definitions of lower- and upper-semicontinuity (under certain
restrictions on the order on the codomain):
* `lowerSemicontinuous_iff_isOpen_preimage` in a linear order;
* `lowerSemicontinuous_iff_isClosed_preimage` in a linear order;
* `lowerSemicontinuousAt_iff_le_liminf` in a complete linear order;
* `lowerSemicontinuous_iff_isClosed_epigraph` in a linear order with the order
  topology.

We also prove:

* `indicator s (fun _ â†¦ y)` is lower semicontinuous when `s` is open and `0 â‰¤ y`,
  or when `s` is closed and `y â‰¤ 0`;
* continuous functions are lower semicontinuous;
* left composition with a continuous monotone functions maps lower semicontinuous functions to lower
  semicontinuous functions. If the function is anti-monotone, it instead maps lower semicontinuous
  functions to upper semicontinuous functions;
* a sum of two (or finitely many) lower semicontinuous functions is lower semicontinuous;
* a supremum of a family of lower semicontinuous functions is lower semicontinuous;
* An infinite sum of `â„â‰¥0âˆ`-valued lower semicontinuous functions is lower semicontinuous.

Similar results are stated and proved for upper semicontinuity.

We also prove that a function is continuous if and only if it is both lower and upper
semicontinuous.

## Implementation details

All the nontrivial results for upper semicontinuous functions are deduced from the corresponding
ones for lower semicontinuous functions using `OrderDual`.

## References

* <https://en.wikipedia.org/wiki/Closed_convex_function>
* <https://en.wikipedia.org/wiki/Semi-continuity>


+ lower and upper semicontinuity correspond to `r := (f Â· > Â·)` and `r := (f Â· < Â·)`;
+ lower and upper hemicontinuity correspond to `r := (fun x s â†¦ IsOpen s âˆ§ ((f x) âˆ© s).Nonempty)`
  and `r := (fun x s â†¦ s âˆˆ ğ“Ë¢ (f x))`, respectively.
-/

public section

open Topology ENNReal

open Set Function Filter

variable {Î± Î² Î³ : Type*} [TopologicalSpace Î±] [TopologicalSpace Î³] {f : Î± â†’ Î²} {s t : Set Î±}
  {x : Î±} {y z : Î²}

/-! ### lower bounds -/

section

variable [LinearOrder Î²]

/-- A lower semicontinuous function attains its lower bound on a nonempty compact set. -/
theorem LowerSemicontinuousOn.exists_isMinOn {s : Set Î±} (ne_s : s.Nonempty)
    (hs : IsCompact s) (hf : LowerSemicontinuousOn f s) :
    âˆƒ a âˆˆ s, IsMinOn f s a := by
  simp only [isMinOn_iff]
  have _ : Nonempty Î± := Exists.nonempty ne_s
  have _ : Nonempty s := Nonempty.to_subtype ne_s
  let Ï† : Î² â†’ Filter Î± := fun b â†¦ ğ“Ÿ (s âˆ© f â»Â¹' Iic b)
  let â„± : Filter Î± := â¨… a : s, Ï† (f a)
  have : â„±.NeBot := by
    apply iInf_neBot_of_directed _ _
    Â· change Directed GE.ge (fun x â†¦ (Ï† âˆ˜ (fun (a : s) â†¦ f â†‘a)) x)
      exact Directed.mono_comp GE.ge (fun x y hxy â†¦
        principal_mono.mpr (inter_subset_inter_right _ (preimage_mono <| Iic_subset_Iic.mpr hxy)))
        (Std.Total.directed _)
    Â· intro x
      have : (pure x : Filter Î±) â‰¤ Ï† (f x) := le_principal_iff.mpr âŸ¨x.2, le_refl (f x)âŸ©
      exact neBot_of_le this
  have hâ„±s : â„± â‰¤ ğ“Ÿ s :=
    iInf_le_of_le (Classical.choice inferInstance) (principal_mono.mpr <| inter_subset_left)
  have hâ„± (x) (hx : x âˆˆ s) : âˆ€á¶  y in â„±, f y â‰¤ f x :=
    mem_iInf_of_mem âŸ¨x, hxâŸ© (by apply inter_subset_right)
  obtain âŸ¨a, ha, hâŸ© := hs hâ„±s
  refine âŸ¨a, ha, fun x hx â†¦ le_of_not_gt fun hxa â†¦ ?_âŸ©
  let _ : (ğ“ a âŠ“ â„±).NeBot := h
  suffices âˆ€á¶  _ in ğ“ a âŠ“ â„±, False by rwa [eventually_const] at this
  filter_upwards [(hf a ha (f x) hxa).filter_mono (inf_le_inf_left _ hâ„±s),
    (hâ„± x hx).filter_mono (inf_le_right : ğ“ a âŠ“ â„± â‰¤ â„±)] using fun y hâ‚ hâ‚‚ â†¦ not_le_of_gt hâ‚ hâ‚‚

/-- A lower semicontinuous function is bounded below on a compact set. -/
theorem LowerSemicontinuousOn.bddBelow_of_isCompact [Nonempty Î²] {s : Set Î±} (hs : IsCompact s)
    (hf : LowerSemicontinuousOn f s) : BddBelow (f '' s) := by
  cases s.eq_empty_or_nonempty with
  | inl h =>
      simp only [h, Set.image_empty]
      exact bddBelow_empty
  | inr h =>
      obtain âŸ¨a, _, hasâŸ© := LowerSemicontinuousOn.exists_isMinOn h hs hf
      exact has.bddBelow

end

/-! #### Indicators -/


section

variable [Zero Î²] [Preorder Î²]

theorem IsOpen.lowerSemicontinuous_indicator (hs : IsOpen s) (hy : 0 â‰¤ y) :
    LowerSemicontinuous (indicator s fun _x => y) := by
  intro x z hz
  by_cases h : x âˆˆ s <;> simp [h] at hz
  Â· filter_upwards [hs.mem_nhds h]
    simp +contextual [hz]
  Â· refine Filter.Eventually.of_forall fun x' => ?_
    by_cases h' : x' âˆˆ s <;> simp [h', hz.trans_le hy, hz]

theorem IsOpen.lowerSemicontinuousOn_indicator (hs : IsOpen s) (hy : 0 â‰¤ y) :
    LowerSemicontinuousOn (indicator s fun _x => y) t :=
  (hs.lowerSemicontinuous_indicator hy).lowerSemicontinuousOn t

theorem IsOpen.lowerSemicontinuousAt_indicator (hs : IsOpen s) (hy : 0 â‰¤ y) :
    LowerSemicontinuousAt (indicator s fun _x => y) x :=
  (hs.lowerSemicontinuous_indicator hy).lowerSemicontinuousAt x

theorem IsOpen.lowerSemicontinuousWithinAt_indicator (hs : IsOpen s) (hy : 0 â‰¤ y) :
    LowerSemicontinuousWithinAt (indicator s fun _x => y) t x :=
  (hs.lowerSemicontinuous_indicator hy).lowerSemicontinuousWithinAt t x

theorem IsClosed.lowerSemicontinuous_indicator (hs : IsClosed s) (hy : y â‰¤ 0) :
    LowerSemicontinuous (indicator s fun _x => y) := by
  intro x z hz
  by_cases h : x âˆˆ s <;> simp [h] at hz
  Â· refine Filter.Eventually.of_forall fun x' => ?_
    by_cases h' : x' âˆˆ s <;> simp [h', hz, hz.trans_le hy]
  Â· filter_upwards [hs.isOpen_compl.mem_nhds h]
    simp +contextual [hz]

theorem IsClosed.lowerSemicontinuousOn_indicator (hs : IsClosed s) (hy : y â‰¤ 0) :
    LowerSemicontinuousOn (indicator s fun _x => y) t :=
  (hs.lowerSemicontinuous_indicator hy).lowerSemicontinuousOn t

theorem IsClosed.lowerSemicontinuousAt_indicator (hs : IsClosed s) (hy : y â‰¤ 0) :
    LowerSemicontinuousAt (indicator s fun _x => y) x :=
  (hs.lowerSemicontinuous_indicator hy).lowerSemicontinuousAt x

theorem IsClosed.lowerSemicontinuousWithinAt_indicator (hs : IsClosed s) (hy : y â‰¤ 0) :
    LowerSemicontinuousWithinAt (indicator s fun _x => y) t x :=
  (hs.lowerSemicontinuous_indicator hy).lowerSemicontinuousWithinAt t x

end

/-! #### Relationship with continuity -/

section

variable [Preorder Î²]

theorem lowerSemicontinuous_iff_isOpen_preimage :
    LowerSemicontinuous f â†” âˆ€ y, IsOpen (f â»Â¹' Ioi y) :=
  âŸ¨fun H y => isOpen_iff_mem_nhds.2 fun x hx => H x y hx, fun H _x y y_lt =>
    IsOpen.mem_nhds (H y) y_ltâŸ©

theorem LowerSemicontinuous.isOpen_preimage (hf : LowerSemicontinuous f) (y : Î²) :
    IsOpen (f â»Â¹' Ioi y) :=
  lowerSemicontinuous_iff_isOpen_preimage.1 hf y

theorem lowerSemicontinuousOn_iff_preimage_Ioi :
    LowerSemicontinuousOn f s â†” âˆ€ b, âˆƒ u, IsOpen u âˆ§ s âˆ© f â»Â¹' Set.Ioi b = s âˆ© u := by
  simp only [â† lowerSemicontinuous_restrict_iff, restrict_eq,
    lowerSemicontinuous_iff_isOpen_preimage, preimage_comp, isOpen_induced_iff,
    Subtype.preimage_coe_eq_preimage_coe_iff, eq_comm]

end

section

variable {Î³ : Type*} [LinearOrder Î³]

theorem lowerSemicontinuous_iff_isClosed_preimage {f : Î± â†’ Î³} :
    LowerSemicontinuous f â†” âˆ€ y, IsClosed (f â»Â¹' Iic y) := by
  rw [lowerSemicontinuous_iff_isOpen_preimage]
  simp only [â† isOpen_compl_iff, â† preimage_compl, compl_Iic]

theorem LowerSemicontinuous.isClosed_preimage {f : Î± â†’ Î³} (hf : LowerSemicontinuous f) (y : Î³) :
    IsClosed (f â»Â¹' Iic y) :=
  lowerSemicontinuous_iff_isClosed_preimage.1 hf y

theorem lowerSemicontinuousOn_iff_preimage_Iic {f : Î± â†’ Î³} :
    LowerSemicontinuousOn f s â†” âˆ€ b, âˆƒ v, IsClosed v âˆ§ s âˆ© f â»Â¹' Set.Iic b = s âˆ© v := by
  simp only [â† lowerSemicontinuous_restrict_iff, restrict_eq,
      lowerSemicontinuous_iff_isClosed_preimage, preimage_comp,
      isClosed_induced_iff, Subtype.preimage_coe_eq_preimage_coe_iff, eq_comm]

variable [TopologicalSpace Î³] [OrderTopology Î³]

theorem ContinuousWithinAt.lowerSemicontinuousWithinAt {f : Î± â†’ Î³} (h : ContinuousWithinAt f s x) :
    LowerSemicontinuousWithinAt f s x := fun _y hy => h (Ioi_mem_nhds hy)

theorem ContinuousAt.lowerSemicontinuousAt {f : Î± â†’ Î³} (h : ContinuousAt f x) :
    LowerSemicontinuousAt f x := fun _y hy => h (Ioi_mem_nhds hy)

theorem ContinuousOn.lowerSemicontinuousOn {f : Î± â†’ Î³} (h : ContinuousOn f s) :
    LowerSemicontinuousOn f s := fun x hx => (h x hx).lowerSemicontinuousWithinAt

theorem Continuous.lowerSemicontinuous {f : Î± â†’ Î³} (h : Continuous f) : LowerSemicontinuous f :=
  fun _x => h.continuousAt.lowerSemicontinuousAt

end

/-! #### Equivalent definitions -/

section

variable {Î³ : Type*} [CompleteLinearOrder Î³]

theorem lowerSemicontinuousWithinAt_iff_le_liminf {f : Î± â†’ Î³} :
    LowerSemicontinuousWithinAt f s x â†” f x â‰¤ liminf f (ğ“[s] x) := by
  constructor
  Â· intro h; unfold LowerSemicontinuousWithinAt at h
    by_contra! hf
    obtain âŸ¨z, ltz, y, ylt, hâ‚âŸ© := hf.exists_disjoint_Iio_Ioi
    exact ltz.not_ge
      (le_liminf_of_le (by isBoundedDefault) ((h y ylt).mono fun _ hâ‚‚ =>
        le_of_not_gt fun hâ‚ƒ => (hâ‚ _ hâ‚ƒ _ hâ‚‚).false))
  exact fun hf y ylt => eventually_lt_of_lt_liminf (ylt.trans_le hf)

alias âŸ¨LowerSemicontinuousWithinAt.le_liminf, _âŸ© := lowerSemicontinuousWithinAt_iff_le_liminf

theorem lowerSemicontinuousAt_iff_le_liminf {f : Î± â†’ Î³} :
    LowerSemicontinuousAt f x â†” f x â‰¤ liminf f (ğ“ x) := by
  rw [â† lowerSemicontinuousWithinAt_univ_iff, lowerSemicontinuousWithinAt_iff_le_liminf,
    â† nhdsWithin_univ]

alias âŸ¨LowerSemicontinuousAt.le_liminf, _âŸ© := lowerSemicontinuousAt_iff_le_liminf

theorem lowerSemicontinuous_iff_le_liminf {f : Î± â†’ Î³} :
    LowerSemicontinuous f â†” âˆ€ x, f x â‰¤ liminf f (ğ“ x) := by
  simp only [â† lowerSemicontinuousAt_iff_le_liminf, lowerSemicontinuous_iff]

alias âŸ¨LowerSemicontinuous.le_liminf, _âŸ© := lowerSemicontinuous_iff_le_liminf

theorem lowerSemicontinuousOn_iff_le_liminf {f : Î± â†’ Î³} :
    LowerSemicontinuousOn f s â†” âˆ€ x âˆˆ s, f x â‰¤ liminf f (ğ“[s] x) := by
  simp only [â† lowerSemicontinuousWithinAt_iff_le_liminf, lowerSemicontinuousOn_iff]

alias âŸ¨LowerSemicontinuousOn.le_liminf, _âŸ© := lowerSemicontinuousOn_iff_le_liminf

end

section

variable {Î³ : Type*} [LinearOrder Î³]

/-- The sublevel sets of a lower semicontinuous function on a compact set are compact. -/
theorem LowerSemicontinuousOn.isCompact_inter_preimage_Iic {f : Î± â†’ Î³}
    (hfs : LowerSemicontinuousOn f s) (ks : IsCompact s) (c : Î³) :
    IsCompact (s âˆ© f â»Â¹' Iic c) := by
  rw [lowerSemicontinuousOn_iff_preimage_Iic] at hfs
  obtain âŸ¨v, hv, hv'âŸ© := hfs c
  exact hv' â–¸ ks.inter_right hv

open scoped Set.Notation in
/-- An intersection of sublevel sets of a lower semicontinuous function
on a compact set is empty if and only if a finite sub-intersection is already empty. -/
theorem LowerSemicontinuousOn.inter_biInter_preimage_Iic_eq_empty_iff_exists_finset
    {Î¹ : Type*} {f : Î¹ â†’ Î± â†’ Î³}
    (ks : IsCompact s) {I : Set Î¹} {c : Î³} (hfi : âˆ€ i âˆˆ I, LowerSemicontinuousOn (f i) s) :
    s âˆ© â‹‚ i âˆˆ I, (f i) â»Â¹' Iic c = âˆ… â†” âˆƒ u : Finset I, âˆ€ x âˆˆ s, âˆƒ i âˆˆ u, c < f i x := by
  refine âŸ¨fun H â†¦ ?_, fun âŸ¨u, huâŸ© â†¦ ?_âŸ©
  Â· suffices âˆ€ i âˆˆ I, IsClosed (s â†“âˆ© (fun i â†¦ f i â»Â¹' Iic c) i) by
      simpa [Set.eq_empty_iff_forall_notMem] using
        ks.elim_finite_subfamily_isClosed_subtype _ this H
    exact fun i hi â†¦ lowerSemicontinuous_restrict_iff.mpr (hfi i hi) |>.isClosed_preimage c
  Â· rw [Set.eq_empty_iff_forall_notMem]
    simp only [mem_inter_iff, mem_iInter, mem_preimage, mem_Iic, not_and, not_forall,
      exists_prop, not_le]
    grind

variable [TopologicalSpace Î³] [ClosedIciTopology Î³]

theorem lowerSemicontinuousOn_iff_isClosed_epigraph {f : Î± â†’ Î³} {s : Set Î±} (hs : IsClosed s) :
    LowerSemicontinuousOn f s â†” IsClosed {p : Î± Ã— Î³ | p.1 âˆˆ s âˆ§ f p.1 â‰¤ p.2} := by
  simp_rw [lowerSemicontinuousOn_iff, lowerSemicontinuousWithinAt_iff,
    eventually_nhdsWithin_iff, â† isOpen_compl_iff, compl_setOf, isOpen_iff_eventually, mem_setOf,
    not_and, not_le]
  constructor
  Â· intro hf âŸ¨x, yâŸ© h
    by_cases hx : x âˆˆ s
    Â· have âŸ¨y', hy', z, hz, hâ‚âŸ© := (h hx).exists_disjoint_Iio_Ioi
      filter_upwards [(hf x hx z hz).prodMk_nhds (eventually_lt_nhds hy')]
        with _ âŸ¨hâ‚‚, hâ‚ƒâŸ© hâ‚„ using hâ‚ _ hâ‚ƒ _ <| hâ‚‚ hâ‚„
    Â· filter_upwards [(continuous_fst.tendsto _).eventually (hs.isOpen_compl.eventually_mem hx)]
        with _ hâ‚ hâ‚‚ using (hâ‚ hâ‚‚).elim
  Â· intro hf x _ y hy
    exact ((Continuous.prodMk_left y).tendsto x).eventually (hf (x, y) (fun _ => hy))

theorem lowerSemicontinuous_iff_isClosed_epigraph {f : Î± â†’ Î³} :
    LowerSemicontinuous f â†” IsClosed {p : Î± Ã— Î³ | f p.1 â‰¤ p.2} := by
  simp [â† lowerSemicontinuousOn_univ_iff, lowerSemicontinuousOn_iff_isClosed_epigraph]

alias âŸ¨LowerSemicontinuous.isClosed_epigraph, _âŸ© := lowerSemicontinuous_iff_isClosed_epigraph

end

/-! ### Composition -/

section

variable [Preorder Î²]
variable {Î³ : Type*} [LinearOrder Î³] [TopologicalSpace Î³] [OrderTopology Î³]
variable {Î´ : Type*} [LinearOrder Î´] [TopologicalSpace Î´] [OrderTopology Î´]
variable {Î¹ : Type*} [TopologicalSpace Î¹]

theorem ContinuousAt.comp_lowerSemicontinuousWithinAt {g : Î³ â†’ Î´} {f : Î± â†’ Î³}
    (hg : ContinuousAt g (f x)) (hf : LowerSemicontinuousWithinAt f s x) (gmon : Monotone g) :
    LowerSemicontinuousWithinAt (g âˆ˜ f) s x := by
  intro y hy
  by_cases! h : âˆƒ l, l < f x
  Â· obtain âŸ¨z, zlt, hzâŸ© : âˆƒ z < f x, Ioc z (f x) âŠ† g â»Â¹' Ioi y :=
      exists_Ioc_subset_of_mem_nhds (hg (Ioi_mem_nhds hy)) h
    filter_upwards [hf z zlt] with a ha
    calc
      y < g (min (f x) (f a)) := hz (by simp [zlt, ha])
      _ â‰¤ g (f a) := gmon (min_le_right _ _)
  Â· exact Filter.Eventually.of_forall fun a => hy.trans_le (gmon (h (f a)))

theorem ContinuousAt.comp_lowerSemicontinuousAt {g : Î³ â†’ Î´} {f : Î± â†’ Î³} (hg : ContinuousAt g (f x))
    (hf : LowerSemicontinuousAt f x) (gmon : Monotone g) : LowerSemicontinuousAt (g âˆ˜ f) x := by
  simp only [â† lowerSemicontinuousWithinAt_univ_iff] at hf âŠ¢
  exact hg.comp_lowerSemicontinuousWithinAt hf gmon

theorem Continuous.comp_lowerSemicontinuousOn {g : Î³ â†’ Î´} {f : Î± â†’ Î³} (hg : Continuous g)
    (hf : LowerSemicontinuousOn f s) (gmon : Monotone g) : LowerSemicontinuousOn (g âˆ˜ f) s :=
  fun x hx => hg.continuousAt.comp_lowerSemicontinuousWithinAt (hf x hx) gmon

theorem Continuous.comp_lowerSemicontinuous {g : Î³ â†’ Î´} {f : Î± â†’ Î³} (hg : Continuous g)
    (hf : LowerSemicontinuous f) (gmon : Monotone g) : LowerSemicontinuous (g âˆ˜ f) := fun x =>
  hg.continuousAt.comp_lowerSemicontinuousAt (hf x) gmon

theorem ContinuousAt.comp_lowerSemicontinuousWithinAt_antitone {g : Î³ â†’ Î´} {f : Î± â†’ Î³}
    (hg : ContinuousAt g (f x)) (hf : LowerSemicontinuousWithinAt f s x) (gmon : Antitone g) :
    UpperSemicontinuousWithinAt (g âˆ˜ f) s x :=
  ContinuousAt.comp_lowerSemicontinuousWithinAt (Î´ := Î´áµ’áµˆ) hg hf gmon

theorem ContinuousAt.comp_lowerSemicontinuousAt_antitone {g : Î³ â†’ Î´} {f : Î± â†’ Î³}
    (hg : ContinuousAt g (f x)) (hf : LowerSemicontinuousAt f x) (gmon : Antitone g) :
    UpperSemicontinuousAt (g âˆ˜ f) x :=
  ContinuousAt.comp_lowerSemicontinuousAt (Î´ := Î´áµ’áµˆ) hg hf gmon

theorem Continuous.comp_lowerSemicontinuousOn_antitone {g : Î³ â†’ Î´} {f : Î± â†’ Î³} (hg : Continuous g)
    (hf : LowerSemicontinuousOn f s) (gmon : Antitone g) : UpperSemicontinuousOn (g âˆ˜ f) s :=
  fun x hx => hg.continuousAt.comp_lowerSemicontinuousWithinAt_antitone (hf x hx) gmon

theorem Continuous.comp_lowerSemicontinuous_antitone {g : Î³ â†’ Î´} {f : Î± â†’ Î³} (hg : Continuous g)
    (hf : LowerSemicontinuous f) (gmon : Antitone g) : UpperSemicontinuous (g âˆ˜ f) := fun x =>
  hg.continuousAt.comp_lowerSemicontinuousAt_antitone (hf x) gmon

@[deprecated (since := "2025-12-06")]
alias LowerSemicontinuousAt.comp_continuousAt := LowerSemicontinuousAt.comp

@[deprecated (since := "2025-12-06")]
alias LowerSemicontinuousAt.comp_continuousAt_of_eq := LowerSemicontinuousAt.comp

@[deprecated (since := "2025-12-06")]
alias LowerSemicontinuous.comp_continuous := LowerSemicontinuous.comp

end

/-! #### Addition -/


section

variable {Î¹ : Type*} {Î³ : Type*} [AddCommMonoid Î³] [LinearOrder Î³] [IsOrderedAddMonoid Î³]
  [TopologicalSpace Î³] [OrderTopology Î³]

/-- The sum of two lower semicontinuous functions is lower semicontinuous. Formulated with an
explicit continuity assumption on addition, for application to `EReal`. The unprimed version of
the lemma uses `[ContinuousAdd]`. -/
theorem LowerSemicontinuousWithinAt.add' {f g : Î± â†’ Î³} (hf : LowerSemicontinuousWithinAt f s x)
    (hg : LowerSemicontinuousWithinAt g s x)
    (hcont : ContinuousAt (fun p : Î³ Ã— Î³ => p.1 + p.2) (f x, g x)) :
    LowerSemicontinuousWithinAt (fun z => f z + g z) s x := by
  intro y hy
  obtain âŸ¨u, v, u_open, xu, v_open, xv, hâŸ© :
    âˆƒ u v : Set Î³,
      IsOpen u âˆ§ f x âˆˆ u âˆ§ IsOpen v âˆ§ g x âˆˆ v âˆ§ u Ã—Ë¢ v âŠ† { p : Î³ Ã— Î³ | y < p.fst + p.snd } :=
    mem_nhds_prod_iff'.1 (hcont (isOpen_Ioi.mem_nhds hy))
  by_cases hxâ‚ : âˆƒ l, l < f x
  Â· obtain âŸ¨zâ‚, zâ‚lt, hâ‚âŸ© : âˆƒ zâ‚ < f x, Ioc zâ‚ (f x) âŠ† u :=
      exists_Ioc_subset_of_mem_nhds (u_open.mem_nhds xu) hxâ‚
    by_cases hxâ‚‚ : âˆƒ l, l < g x
    Â· obtain âŸ¨zâ‚‚, zâ‚‚lt, hâ‚‚âŸ© : âˆƒ zâ‚‚ < g x, Ioc zâ‚‚ (g x) âŠ† v :=
        exists_Ioc_subset_of_mem_nhds (v_open.mem_nhds xv) hxâ‚‚
      filter_upwards [hf zâ‚ zâ‚lt, hg zâ‚‚ zâ‚‚lt] with z hâ‚z hâ‚‚z
      have A1 : min (f z) (f x) âˆˆ u := by
        by_cases! H : f z â‰¤ f x
        Â· simpa [H] using hâ‚ âŸ¨hâ‚z, HâŸ©
        Â· simpa [H.le]
      have A2 : min (g z) (g x) âˆˆ v := by
        by_cases! H : g z â‰¤ g x
        Â· simpa [H] using hâ‚‚ âŸ¨hâ‚‚z, HâŸ©
        Â· simpa [H.le]
      have : (min (f z) (f x), min (g z) (g x)) âˆˆ u Ã—Ë¢ v := âŸ¨A1, A2âŸ©
      calc
        y < min (f z) (f x) + min (g z) (g x) := h this
        _ â‰¤ f z + g z := add_le_add (min_le_left _ _) (min_le_left _ _)
    Â· simp only [not_exists, not_lt] at hxâ‚‚
      filter_upwards [hf zâ‚ zâ‚lt] with z hâ‚z
      have A1 : min (f z) (f x) âˆˆ u := by
        by_cases! H : f z â‰¤ f x
        Â· simpa [H] using hâ‚ âŸ¨hâ‚z, HâŸ©
        Â· simpa [H.le]
      have : (min (f z) (f x), g x) âˆˆ u Ã—Ë¢ v := âŸ¨A1, xvâŸ©
      calc
        y < min (f z) (f x) + g x := h this
        _ â‰¤ f z + g z := add_le_add (min_le_left _ _) (hxâ‚‚ (g z))
  Â· simp only [not_exists, not_lt] at hxâ‚
    by_cases hxâ‚‚ : âˆƒ l, l < g x
    Â· obtain âŸ¨zâ‚‚, zâ‚‚lt, hâ‚‚âŸ© : âˆƒ zâ‚‚ < g x, Ioc zâ‚‚ (g x) âŠ† v :=
        exists_Ioc_subset_of_mem_nhds (v_open.mem_nhds xv) hxâ‚‚
      filter_upwards [hg zâ‚‚ zâ‚‚lt] with z hâ‚‚z
      have A2 : min (g z) (g x) âˆˆ v := by
        by_cases! H : g z â‰¤ g x
        Â· simpa [H] using hâ‚‚ âŸ¨hâ‚‚z, HâŸ©
        Â· simpa [H.le] using hâ‚‚ âŸ¨zâ‚‚lt, le_rflâŸ©
      have : (f x, min (g z) (g x)) âˆˆ u Ã—Ë¢ v := âŸ¨xu, A2âŸ©
      calc
        y < f x + min (g z) (g x) := h this
        _ â‰¤ f z + g z := add_le_add (hxâ‚ (f z)) (min_le_left _ _)
    Â· simp only [not_exists, not_lt] at hxâ‚ hxâ‚‚
      apply Filter.Eventually.of_forall
      intro z
      have : (f x, g x) âˆˆ u Ã—Ë¢ v := âŸ¨xu, xvâŸ©
      calc
        y < f x + g x := h this
        _ â‰¤ f z + g z := add_le_add (hxâ‚ (f z)) (hxâ‚‚ (g z))

/-- The sum of two lower semicontinuous functions is lower semicontinuous. Formulated with an
explicit continuity assumption on addition, for application to `EReal`. The unprimed version of
the lemma uses `[ContinuousAdd]`. -/
theorem LowerSemicontinuousAt.add' {f g : Î± â†’ Î³} (hf : LowerSemicontinuousAt f x)
    (hg : LowerSemicontinuousAt g x)
    (hcont : ContinuousAt (fun p : Î³ Ã— Î³ => p.1 + p.2) (f x, g x)) :
    LowerSemicontinuousAt (fun z => f z + g z) x := by
  simp_rw [â† lowerSemicontinuousWithinAt_univ_iff] at *
  exact hf.add' hg hcont

/-- The sum of two lower semicontinuous functions is lower semicontinuous. Formulated with an
explicit continuity assumption on addition, for application to `EReal`. The unprimed version of
the lemma uses `[ContinuousAdd]`. -/
theorem LowerSemicontinuousOn.add' {f g : Î± â†’ Î³} (hf : LowerSemicontinuousOn f s)
    (hg : LowerSemicontinuousOn g s)
    (hcont : âˆ€ x âˆˆ s, ContinuousAt (fun p : Î³ Ã— Î³ => p.1 + p.2) (f x, g x)) :
    LowerSemicontinuousOn (fun z => f z + g z) s := fun x hx =>
  LowerSemicontinuousWithinAt.add' (hf x hx) (hg x hx) (hcont x hx)

/-- The sum of two lower semicontinuous functions is lower semicontinuous. Formulated with an
explicit continuity assumption on addition, for application to `EReal`. The unprimed version of
the lemma uses `[ContinuousAdd]`. -/
theorem LowerSemicontinuous.add' {f g : Î± â†’ Î³} (hf : LowerSemicontinuous f)
    (hg : LowerSemicontinuous g)
    (hcont : âˆ€ x, ContinuousAt (fun p : Î³ Ã— Î³ => p.1 + p.2) (f x, g x)) :
    LowerSemicontinuous fun z => f z + g z :=
  fun x => LowerSemicontinuousAt.add' (hf x) (hg x) (hcont x)

variable [ContinuousAdd Î³]

/-- The sum of two lower semicontinuous functions is lower semicontinuous. Formulated with
`[ContinuousAdd]`. The primed version of the lemma uses an explicit continuity assumption on
addition, for application to `EReal`. -/
theorem LowerSemicontinuousWithinAt.add {f g : Î± â†’ Î³} (hf : LowerSemicontinuousWithinAt f s x)
    (hg : LowerSemicontinuousWithinAt g s x) :
    LowerSemicontinuousWithinAt (fun z => f z + g z) s x :=
  hf.add' hg continuous_add.continuousAt

/-- The sum of two lower semicontinuous functions is lower semicontinuous. Formulated with
`[ContinuousAdd]`. The primed version of the lemma uses an explicit continuity assumption on
addition, for application to `EReal`. -/
theorem LowerSemicontinuousAt.add {f g : Î± â†’ Î³} (hf : LowerSemicontinuousAt f x)
    (hg : LowerSemicontinuousAt g x) : LowerSemicontinuousAt (fun z => f z + g z) x :=
  hf.add' hg continuous_add.continuousAt

/-- The sum of two lower semicontinuous functions is lower semicontinuous. Formulated with
`[ContinuousAdd]`. The primed version of the lemma uses an explicit continuity assumption on
addition, for application to `EReal`. -/
theorem LowerSemicontinuousOn.add {f g : Î± â†’ Î³} (hf : LowerSemicontinuousOn f s)
    (hg : LowerSemicontinuousOn g s) : LowerSemicontinuousOn (fun z => f z + g z) s :=
  hf.add' hg fun _x _hx => continuous_add.continuousAt

/-- The sum of two lower semicontinuous functions is lower semicontinuous. Formulated with
`[ContinuousAdd]`. The primed version of the lemma uses an explicit continuity assumption on
addition, for application to `EReal`. -/
theorem LowerSemicontinuous.add {f g : Î± â†’ Î³} (hf : LowerSemicontinuous f)
    (hg : LowerSemicontinuous g) : LowerSemicontinuous fun z => f z + g z :=
  hf.add' hg fun _x => continuous_add.continuousAt

theorem lowerSemicontinuousWithinAt_sum {f : Î¹ â†’ Î± â†’ Î³} {a : Finset Î¹}
    (ha : âˆ€ i âˆˆ a, LowerSemicontinuousWithinAt (f i) s x) :
    LowerSemicontinuousWithinAt (fun z => âˆ‘ i âˆˆ a, f i z) s x := by
  classical
    induction a using Finset.induction_on with
    | empty => exact lowerSemicontinuousWithinAt_const
    | insert _ _ ia IH =>
      simp only [ia, Finset.sum_insert, not_false_iff]
      exact
        LowerSemicontinuousWithinAt.add (ha _ (Finset.mem_insert_self ..))
          (IH fun j ja => ha j (Finset.mem_insert_of_mem ja))

theorem lowerSemicontinuousAt_sum {f : Î¹ â†’ Î± â†’ Î³} {a : Finset Î¹}
    (ha : âˆ€ i âˆˆ a, LowerSemicontinuousAt (f i) x) :
    LowerSemicontinuousAt (fun z => âˆ‘ i âˆˆ a, f i z) x := by
  simp_rw [â† lowerSemicontinuousWithinAt_univ_iff] at *
  exact lowerSemicontinuousWithinAt_sum ha

theorem lowerSemicontinuousOn_sum {f : Î¹ â†’ Î± â†’ Î³} {a : Finset Î¹}
    (ha : âˆ€ i âˆˆ a, LowerSemicontinuousOn (f i) s) :
    LowerSemicontinuousOn (fun z => âˆ‘ i âˆˆ a, f i z) s := fun x hx =>
  lowerSemicontinuousWithinAt_sum fun i hi => ha i hi x hx

theorem lowerSemicontinuous_sum {f : Î¹ â†’ Î± â†’ Î³} {a : Finset Î¹}
    (ha : âˆ€ i âˆˆ a, LowerSemicontinuous (f i)) : LowerSemicontinuous fun z => âˆ‘ i âˆˆ a, f i z :=
  fun x => lowerSemicontinuousAt_sum fun i hi => ha i hi x

end

/-! #### Supremum -/

section

variable {Î± : Type*} {Î² : Type*} [TopologicalSpace Î±] [LinearOrder Î²]
  {f g : Î± â†’ Î²} {s : Set Î±} {a : Î±}

theorem LowerSemicontinuousWithinAt.sup
    (hf : LowerSemicontinuousWithinAt f s a) (hg : LowerSemicontinuousWithinAt g s a) :
    LowerSemicontinuousWithinAt (fun x â†¦ f x âŠ” g x) s a := by
  intro b hb
  simp only [lt_sup_iff] at hb âŠ¢
  rcases hb with hb | hb
  Â· filter_upwards [hf b hb] with x using Or.intro_left _
  Â· filter_upwards [hg b hb] with x using Or.intro_right _

theorem LowerSemicontinuousAt.sup
    (hf : LowerSemicontinuousAt f a) (hg : LowerSemicontinuousAt g a) :
    LowerSemicontinuousAt (fun x â†¦ f x âŠ” g x) a := by
  rw [â† lowerSemicontinuousWithinAt_univ_iff] at *
  exact hf.sup hg

theorem LowerSemicontinuousOn.sup
    (hf : LowerSemicontinuousOn f s) (hg : LowerSemicontinuousOn g s) :
    LowerSemicontinuousOn (fun x â†¦ f x âŠ” g x) s := fun a ha â†¦
  LowerSemicontinuousWithinAt.sup (hf a ha) (hg a ha)

theorem LowerSemicontinuous.sup
    (hf : LowerSemicontinuous f) (hg : LowerSemicontinuous g) :
    LowerSemicontinuous fun x â†¦ f x âŠ” g x := fun a â†¦
  LowerSemicontinuousAt.sup (hf a) (hg a)

theorem LowerSemicontinuousWithinAt.inf
    (hf : LowerSemicontinuousWithinAt f s a) (hg : LowerSemicontinuousWithinAt g s a) :
    LowerSemicontinuousWithinAt (fun x â†¦ f x âŠ“ g x) s a := by
  intro b hb
  simp only [lt_inf_iff] at hb âŠ¢
  exact Eventually.and (hf b hb.1) (hg b hb.2)

theorem LowerSemicontinuousAt.inf
    (hf : LowerSemicontinuousAt f a) (hg : LowerSemicontinuousAt g a) :
    LowerSemicontinuousAt (fun x â†¦ f x âŠ“ g x) a := by
  rw [â† lowerSemicontinuousWithinAt_univ_iff] at *
  exact hf.inf hg

theorem LowerSemicontinuousOn.inf
    (hf : LowerSemicontinuousOn f s) (hg : LowerSemicontinuousOn g s) :
    LowerSemicontinuousOn (fun x â†¦ f x âŠ“ g x) s := fun a ha â†¦
  LowerSemicontinuousWithinAt.inf (hf a ha) (hg a ha)

theorem LowerSemicontinuous.inf (hf : LowerSemicontinuous f)
    (hg : LowerSemicontinuous g) :
    LowerSemicontinuous fun x â†¦ f x âŠ“ g x := fun a â†¦
  LowerSemicontinuousAt.inf (hf a) (hg a)

end

section

variable {Î¹ : Sort*} {Î´ Î´' : Type*} [CompleteLinearOrder Î´] [ConditionallyCompleteLinearOrder Î´']

theorem lowerSemicontinuousWithinAt_ciSup {f : Î¹ â†’ Î± â†’ Î´'}
    (bdd : âˆ€á¶  y in ğ“[s] x, BddAbove (range fun i => f i y))
    (h : âˆ€ i, LowerSemicontinuousWithinAt (f i) s x) :
    LowerSemicontinuousWithinAt (fun x' => â¨† i, f i x') s x := by
  cases isEmpty_or_nonempty Î¹
  Â· simpa only [iSup_of_empty'] using lowerSemicontinuousWithinAt_const
  Â· intro y hy
    rcases exists_lt_of_lt_ciSup hy with âŸ¨i, hiâŸ©
    filter_upwards [h i y hi, bdd] with y hy hy' using hy.trans_le (le_ciSup hy' i)

theorem lowerSemicontinuousWithinAt_iSup {f : Î¹ â†’ Î± â†’ Î´}
    (h : âˆ€ i, LowerSemicontinuousWithinAt (f i) s x) :
    LowerSemicontinuousWithinAt (fun x' => â¨† i, f i x') s x :=
  lowerSemicontinuousWithinAt_ciSup (by simp) h

theorem lowerSemicontinuousWithinAt_biSup {p : Î¹ â†’ Prop} {f : âˆ€ i, p i â†’ Î± â†’ Î´}
    (h : âˆ€ i hi, LowerSemicontinuousWithinAt (f i hi) s x) :
    LowerSemicontinuousWithinAt (fun x' => â¨† (i) (hi), f i hi x') s x :=
  lowerSemicontinuousWithinAt_iSup fun i => lowerSemicontinuousWithinAt_iSup fun hi => h i hi

theorem lowerSemicontinuousAt_ciSup {f : Î¹ â†’ Î± â†’ Î´'}
    (bdd : âˆ€á¶  y in ğ“ x, BddAbove (range fun i => f i y)) (h : âˆ€ i, LowerSemicontinuousAt (f i) x) :
    LowerSemicontinuousAt (fun x' => â¨† i, f i x') x := by
  simp_rw [â† lowerSemicontinuousWithinAt_univ_iff] at *
  rw [â† nhdsWithin_univ] at bdd
  exact lowerSemicontinuousWithinAt_ciSup bdd h

theorem lowerSemicontinuousAt_iSup {f : Î¹ â†’ Î± â†’ Î´} (h : âˆ€ i, LowerSemicontinuousAt (f i) x) :
    LowerSemicontinuousAt (fun x' => â¨† i, f i x') x :=
  lowerSemicontinuousAt_ciSup (by simp) h

theorem lowerSemicontinuousAt_biSup {p : Î¹ â†’ Prop} {f : âˆ€ i, p i â†’ Î± â†’ Î´}
    (h : âˆ€ i hi, LowerSemicontinuousAt (f i hi) x) :
    LowerSemicontinuousAt (fun x' => â¨† (i) (hi), f i hi x') x :=
  lowerSemicontinuousAt_iSup fun i => lowerSemicontinuousAt_iSup fun hi => h i hi

theorem lowerSemicontinuousOn_ciSup {f : Î¹ â†’ Î± â†’ Î´'}
    (bdd : âˆ€ x âˆˆ s, BddAbove (range fun i => f i x)) (h : âˆ€ i, LowerSemicontinuousOn (f i) s) :
    LowerSemicontinuousOn (fun x' => â¨† i, f i x') s := fun x hx =>
  lowerSemicontinuousWithinAt_ciSup (eventually_nhdsWithin_of_forall bdd) fun i => h i x hx

theorem lowerSemicontinuousOn_iSup {f : Î¹ â†’ Î± â†’ Î´} (h : âˆ€ i, LowerSemicontinuousOn (f i) s) :
    LowerSemicontinuousOn (fun x' => â¨† i, f i x') s :=
  lowerSemicontinuousOn_ciSup (by simp) h

theorem lowerSemicontinuousOn_biSup {p : Î¹ â†’ Prop} {f : âˆ€ i, p i â†’ Î± â†’ Î´}
    (h : âˆ€ i hi, LowerSemicontinuousOn (f i hi) s) :
    LowerSemicontinuousOn (fun x' => â¨† (i) (hi), f i hi x') s :=
  lowerSemicontinuousOn_iSup fun i => lowerSemicontinuousOn_iSup fun hi => h i hi

theorem lowerSemicontinuous_ciSup {f : Î¹ â†’ Î± â†’ Î´'} (bdd : âˆ€ x, BddAbove (range fun i => f i x))
    (h : âˆ€ i, LowerSemicontinuous (f i)) : LowerSemicontinuous fun x' => â¨† i, f i x' := fun x =>
  lowerSemicontinuousAt_ciSup (Eventually.of_forall bdd) fun i => h i x

theorem lowerSemicontinuous_iSup {f : Î¹ â†’ Î± â†’ Î´} (h : âˆ€ i, LowerSemicontinuous (f i)) :
    LowerSemicontinuous fun x' => â¨† i, f i x' :=
  lowerSemicontinuous_ciSup (by simp) h

theorem lowerSemicontinuous_biSup {p : Î¹ â†’ Prop} {f : âˆ€ i, p i â†’ Î± â†’ Î´}
    (h : âˆ€ i hi, LowerSemicontinuous (f i hi)) :
    LowerSemicontinuous fun x' => â¨† (i) (hi), f i hi x' :=
  lowerSemicontinuous_iSup fun i => lowerSemicontinuous_iSup fun hi => h i hi

end

/-! #### Infinite sums -/


section

variable {Î¹ : Type*}

theorem lowerSemicontinuousWithinAt_tsum {f : Î¹ â†’ Î± â†’ â„â‰¥0âˆ}
    (h : âˆ€ i, LowerSemicontinuousWithinAt (f i) s x) :
    LowerSemicontinuousWithinAt (fun x' => âˆ‘' i, f i x') s x := by
  simp_rw [ENNReal.tsum_eq_iSup_sum]
  refine lowerSemicontinuousWithinAt_iSup fun b => ?_
  exact lowerSemicontinuousWithinAt_sum fun i _hi => h i

theorem lowerSemicontinuousAt_tsum {f : Î¹ â†’ Î± â†’ â„â‰¥0âˆ} (h : âˆ€ i, LowerSemicontinuousAt (f i) x) :
    LowerSemicontinuousAt (fun x' => âˆ‘' i, f i x') x := by
  simp_rw [â† lowerSemicontinuousWithinAt_univ_iff] at *
  exact lowerSemicontinuousWithinAt_tsum h

theorem lowerSemicontinuousOn_tsum {f : Î¹ â†’ Î± â†’ â„â‰¥0âˆ} (h : âˆ€ i, LowerSemicontinuousOn (f i) s) :
    LowerSemicontinuousOn (fun x' => âˆ‘' i, f i x') s := fun x hx =>
  lowerSemicontinuousWithinAt_tsum fun i => h i x hx

theorem lowerSemicontinuous_tsum {f : Î¹ â†’ Î± â†’ â„â‰¥0âˆ} (h : âˆ€ i, LowerSemicontinuous (f i)) :
    LowerSemicontinuous fun x' => âˆ‘' i, f i x' := fun x => lowerSemicontinuousAt_tsum fun i => h i x

end

/-!
### Upper semicontinuous functions
-/

/-! ### upper bounds -/

section

variable {Î± : Type*} [TopologicalSpace Î±] {Î² : Type*} [LinearOrder Î²] {f : Î± â†’ Î²} {s : Set Î±}

/-- An upper semicontinuous function attains its upper bound on a nonempty compact set. -/
theorem UpperSemicontinuousOn.exists_isMaxOn {s : Set Î±} (ne_s : s.Nonempty)
    (hs : IsCompact s) (hf : UpperSemicontinuousOn f s) :
    âˆƒ a âˆˆ s, IsMaxOn f s a :=
  LowerSemicontinuousOn.exists_isMinOn (Î² := Î²áµ’áµˆ) ne_s hs hf

/-- An upper semicontinuous function is bounded above on a compact set. -/
theorem UpperSemicontinuousOn.bddAbove_of_isCompact [Nonempty Î²] {s : Set Î±}
    (hs : IsCompact s) (hf : UpperSemicontinuousOn f s) : BddAbove (f '' s) :=
  LowerSemicontinuousOn.bddBelow_of_isCompact (Î² := Î²áµ’áµˆ) hs hf

end

/-! #### Indicators -/


section

variable [Zero Î²] [Preorder Î²]

theorem IsOpen.upperSemicontinuous_indicator (hs : IsOpen s) (hy : y â‰¤ 0) :
    UpperSemicontinuous (indicator s fun _x => y) :=
  IsOpen.lowerSemicontinuous_indicator (Î² := Î²áµ’áµˆ) hs hy

theorem IsOpen.upperSemicontinuousOn_indicator (hs : IsOpen s) (hy : y â‰¤ 0) :
    UpperSemicontinuousOn (indicator s fun _x => y) t :=
  (hs.upperSemicontinuous_indicator hy).upperSemicontinuousOn t

theorem IsOpen.upperSemicontinuousAt_indicator (hs : IsOpen s) (hy : y â‰¤ 0) :
    UpperSemicontinuousAt (indicator s fun _x => y) x :=
  (hs.upperSemicontinuous_indicator hy).upperSemicontinuousAt x

theorem IsOpen.upperSemicontinuousWithinAt_indicator (hs : IsOpen s) (hy : y â‰¤ 0) :
    UpperSemicontinuousWithinAt (indicator s fun _x => y) t x :=
  (hs.upperSemicontinuous_indicator hy).upperSemicontinuousWithinAt t x

theorem IsClosed.upperSemicontinuous_indicator (hs : IsClosed s) (hy : 0 â‰¤ y) :
    UpperSemicontinuous (indicator s fun _x => y) :=
  IsClosed.lowerSemicontinuous_indicator (Î² := Î²áµ’áµˆ) hs hy

theorem IsClosed.upperSemicontinuousOn_indicator (hs : IsClosed s) (hy : 0 â‰¤ y) :
    UpperSemicontinuousOn (indicator s fun _x => y) t :=
  (hs.upperSemicontinuous_indicator hy).upperSemicontinuousOn t

theorem IsClosed.upperSemicontinuousAt_indicator (hs : IsClosed s) (hy : 0 â‰¤ y) :
    UpperSemicontinuousAt (indicator s fun _x => y) x :=
  (hs.upperSemicontinuous_indicator hy).upperSemicontinuousAt x

theorem IsClosed.upperSemicontinuousWithinAt_indicator (hs : IsClosed s) (hy : 0 â‰¤ y) :
    UpperSemicontinuousWithinAt (indicator s fun _x => y) t x :=
  (hs.upperSemicontinuous_indicator hy).upperSemicontinuousWithinAt t x

end

/-! #### Relationship with continuity -/

section

variable [Preorder Î²]

theorem upperSemicontinuous_iff_isOpen_preimage :
    UpperSemicontinuous f â†” âˆ€ y, IsOpen (f â»Â¹' Iio y) :=
  âŸ¨fun H y => isOpen_iff_mem_nhds.2 fun x hx => H x y hx, fun H _x y y_lt =>
    IsOpen.mem_nhds (H y) y_ltâŸ©

theorem UpperSemicontinuous.isOpen_preimage (hf : UpperSemicontinuous f) (y : Î²) :
    IsOpen (f â»Â¹' Iio y) :=
  upperSemicontinuous_iff_isOpen_preimage.1 hf y

end
section

variable {Î³ : Type*} [LinearOrder Î³]

theorem upperSemicontinuous_iff_isClosed_preimage {f : Î± â†’ Î³} :
    UpperSemicontinuous f â†” âˆ€ y, IsClosed (f â»Â¹' Ici y) := by
  rw [upperSemicontinuous_iff_isOpen_preimage]
  simp only [â† isOpen_compl_iff, â† preimage_compl, compl_Ici]

theorem UpperSemicontinuous.isClosed_preimage {f : Î± â†’ Î³} (hf : UpperSemicontinuous f) (y : Î³) :
    IsClosed (f â»Â¹' Ici y) :=
  upperSemicontinuous_iff_isClosed_preimage.1 hf y

variable [TopologicalSpace Î³] [OrderTopology Î³]

theorem ContinuousWithinAt.upperSemicontinuousWithinAt {f : Î± â†’ Î³} (h : ContinuousWithinAt f s x) :
    UpperSemicontinuousWithinAt f s x := fun _y hy => h (Iio_mem_nhds hy)

theorem ContinuousAt.upperSemicontinuousAt {f : Î± â†’ Î³} (h : ContinuousAt f x) :
    UpperSemicontinuousAt f x := fun _y hy => h (Iio_mem_nhds hy)

theorem ContinuousOn.upperSemicontinuousOn {f : Î± â†’ Î³} (h : ContinuousOn f s) :
    UpperSemicontinuousOn f s := fun x hx => (h x hx).upperSemicontinuousWithinAt

theorem Continuous.upperSemicontinuous {f : Î± â†’ Î³} (h : Continuous f) : UpperSemicontinuous f :=
  fun _x => h.continuousAt.upperSemicontinuousAt

end

/-! #### Equivalent definitions -/

section

variable {Î³ : Type*} [CompleteLinearOrder Î³]

theorem upperSemicontinuousWithinAt_iff_limsup_le {f : Î± â†’ Î³} :
    UpperSemicontinuousWithinAt f s x â†” limsup f (ğ“[s] x) â‰¤ f x :=
  lowerSemicontinuousWithinAt_iff_le_liminf (Î³ := Î³áµ’áµˆ)

alias âŸ¨UpperSemicontinuousWithinAt.limsup_le, _âŸ© := upperSemicontinuousWithinAt_iff_limsup_le

theorem upperSemicontinuousAt_iff_limsup_le {f : Î± â†’ Î³} :
    UpperSemicontinuousAt f x â†” limsup f (ğ“ x) â‰¤ f x :=
  lowerSemicontinuousAt_iff_le_liminf (Î³ := Î³áµ’áµˆ)

alias âŸ¨UpperSemicontinuousAt.limsup_le, _âŸ© := upperSemicontinuousAt_iff_limsup_le

theorem upperSemicontinuous_iff_limsup_le {f : Î± â†’ Î³} :
    UpperSemicontinuous f â†” âˆ€ x, limsup f (ğ“ x) â‰¤ f x :=
  lowerSemicontinuous_iff_le_liminf (Î³ := Î³áµ’áµˆ)

alias âŸ¨UpperSemicontinuous.limsup_le, _âŸ© := upperSemicontinuous_iff_limsup_le

theorem upperSemicontinuousOn_iff_limsup_le {f : Î± â†’ Î³} :
    UpperSemicontinuousOn f s â†” âˆ€ x âˆˆ s, limsup f (ğ“[s] x) â‰¤ f x :=
  lowerSemicontinuousOn_iff_le_liminf (Î³ := Î³áµ’áµˆ)

alias âŸ¨UpperSemicontinuousOn.limsup_le, _âŸ© := upperSemicontinuousOn_iff_limsup_le

end

section

variable {Î³ : Type*} [LinearOrder Î³]

/-- The overlevel sets of an upper semicontinuous function on a compact set are compact. -/
theorem UpperSemicontinuousOn.isCompact_inter_preimage_Ici {f : Î± â†’ Î³}
    (hfs : UpperSemicontinuousOn f s) (ks : IsCompact s) (c : Î³) :
    IsCompact (s âˆ© f â»Â¹' Ici c) :=
  LowerSemicontinuousOn.isCompact_inter_preimage_Iic (Î³ := Î³áµ’áµˆ) hfs ks c

open scoped Set.Notation in
/-- An intersection of overlevel sets of a lower semicontinuous function
on a compact set is empty if and only if a finite sub-intersection is already empty. -/
theorem UpperSemicontinuousOn.inter_biInter_preimage_Ici_eq_empty_iff_exists_finset
    {Î¹ : Type*} {f : Î¹ â†’ Î± â†’ Î³}
    (ks : IsCompact s) {I : Set Î¹} {c : Î³} (hfi : âˆ€ i âˆˆ I, UpperSemicontinuousOn (f i) s) :
    s âˆ© â‹‚ i âˆˆ I, (f i) â»Â¹' Ici c = âˆ… â†” âˆƒ u : Finset I, âˆ€ x âˆˆ s, âˆƒ i âˆˆ u, f i x < c :=
  LowerSemicontinuousOn.inter_biInter_preimage_Iic_eq_empty_iff_exists_finset ks hfi (Î³ := Î³áµ’áµˆ)

variable [TopologicalSpace Î³] [ClosedIicTopology Î³]

theorem upperSemicontinuousOn_iff_isClosed_hypograph {f : Î± â†’ Î³} (hs : IsClosed s) :
    UpperSemicontinuousOn f s â†” IsClosed {p : Î± Ã— Î³ | p.1 âˆˆ s âˆ§ p.2 â‰¤ f p.1} :=
  lowerSemicontinuousOn_iff_isClosed_epigraph hs (Î³ := Î³áµ’áµˆ)

theorem upperSemicontinuous_iff_IsClosed_hypograph {f : Î± â†’ Î³} :
    UpperSemicontinuous f â†” IsClosed {p : Î± Ã— Î³ | p.2 â‰¤ f p.1} :=
  lowerSemicontinuous_iff_isClosed_epigraph (Î³ := Î³áµ’áµˆ)

alias âŸ¨UpperSemicontinuous.IsClosed_hypograph, _âŸ© := upperSemicontinuous_iff_IsClosed_hypograph

end

/-! ### Composition -/

section

variable {Î± : Type*} [TopologicalSpace Î±]
variable {Î² : Type*} [LinearOrder Î²]
variable {Î³ : Type*} [TopologicalSpace Î³]
variable {f : Î± â†’ Î²} {g : Î³ â†’ Î±} {s : Set Î±} {a : Î±} {c : Î³} {t : Set Î³}

theorem upperSemicontinuousOn_iff_preimage_Iio :
    UpperSemicontinuousOn f s â†” âˆ€ b, âˆƒ u : Set Î±, IsOpen u âˆ§ s âˆ© f â»Â¹' Set.Iio b = s âˆ© u :=
  lowerSemicontinuousOn_iff_preimage_Ioi (Î² := Î²áµ’áµˆ)

theorem upperSemicontinuousOn_iff_preimage_Ici :
    UpperSemicontinuousOn f s â†” âˆ€ b, âˆƒ v : Set Î±, IsClosed v âˆ§ s âˆ© f â»Â¹' Set.Ici b = s âˆ© v :=
  lowerSemicontinuousOn_iff_preimage_Iic (Î³ := Î²áµ’áµˆ)

end

section

variable {Î³ : Type*} [LinearOrder Î³] [TopologicalSpace Î³] [OrderTopology Î³]
variable {Î´ : Type*} [LinearOrder Î´] [TopologicalSpace Î´] [OrderTopology Î´]
variable {Î¹ : Type*} [TopologicalSpace Î¹]

theorem ContinuousAt.comp_upperSemicontinuousWithinAt {g : Î³ â†’ Î´} {f : Î± â†’ Î³}
    (hg : ContinuousAt g (f x)) (hf : UpperSemicontinuousWithinAt f s x) (gmon : Monotone g) :
    UpperSemicontinuousWithinAt (g âˆ˜ f) s x :=
  ContinuousAt.comp_lowerSemicontinuousWithinAt (Î³ := Î³áµ’áµˆ) (Î´ := Î´áµ’áµˆ) hg hf gmon.dual

theorem ContinuousAt.comp_upperSemicontinuousAt {g : Î³ â†’ Î´} {f : Î± â†’ Î³} (hg : ContinuousAt g (f x))
    (hf : UpperSemicontinuousAt f x) (gmon : Monotone g) : UpperSemicontinuousAt (g âˆ˜ f) x :=
  ContinuousAt.comp_lowerSemicontinuousAt (Î³ := Î³áµ’áµˆ) (Î´ := Î´áµ’áµˆ) hg hf gmon.dual

theorem Continuous.comp_upperSemicontinuousOn {g : Î³ â†’ Î´} {f : Î± â†’ Î³} (hg : Continuous g)
    (hf : UpperSemicontinuousOn f s) (gmon : Monotone g) : UpperSemicontinuousOn (g âˆ˜ f) s :=
  fun x hx => hg.continuousAt.comp_upperSemicontinuousWithinAt (hf x hx) gmon

theorem Continuous.comp_upperSemicontinuous {g : Î³ â†’ Î´} {f : Î± â†’ Î³} (hg : Continuous g)
    (hf : UpperSemicontinuous f) (gmon : Monotone g) : UpperSemicontinuous (g âˆ˜ f) := fun x =>
  hg.continuousAt.comp_upperSemicontinuousAt (hf x) gmon

theorem ContinuousAt.comp_upperSemicontinuousWithinAt_antitone {g : Î³ â†’ Î´} {f : Î± â†’ Î³}
    (hg : ContinuousAt g (f x)) (hf : UpperSemicontinuousWithinAt f s x) (gmon : Antitone g) :
    LowerSemicontinuousWithinAt (g âˆ˜ f) s x :=
  ContinuousAt.comp_upperSemicontinuousWithinAt (Î´ := Î´áµ’áµˆ) hg hf gmon

theorem ContinuousAt.comp_upperSemicontinuousAt_antitone {g : Î³ â†’ Î´} {f : Î± â†’ Î³}
    (hg : ContinuousAt g (f x)) (hf : UpperSemicontinuousAt f x) (gmon : Antitone g) :
    LowerSemicontinuousAt (g âˆ˜ f) x :=
  ContinuousAt.comp_upperSemicontinuousAt (Î´ := Î´áµ’áµˆ) hg hf gmon

theorem Continuous.comp_upperSemicontinuousOn_antitone {g : Î³ â†’ Î´} {f : Î± â†’ Î³} (hg : Continuous g)
    (hf : UpperSemicontinuousOn f s) (gmon : Antitone g) : LowerSemicontinuousOn (g âˆ˜ f) s :=
  fun x hx => hg.continuousAt.comp_upperSemicontinuousWithinAt_antitone (hf x hx) gmon

theorem Continuous.comp_upperSemicontinuous_antitone {g : Î³ â†’ Î´} {f : Î± â†’ Î³} (hg : Continuous g)
    (hf : UpperSemicontinuous f) (gmon : Antitone g) : LowerSemicontinuous (g âˆ˜ f) := fun x =>
  hg.continuousAt.comp_upperSemicontinuousAt_antitone (hf x) gmon

variable [Preorder Î²]

@[deprecated (since := "2025-12-06")]
alias UpperSemicontinuousAt.comp_continuousAt := UpperSemicontinuousAt.comp

@[deprecated (since := "2025-12-06")]
alias UpperSemicontinuousAt.comp_continuousAt_of_eq := UpperSemicontinuousAt.comp

@[deprecated (since := "2025-12-06")]
alias UpperSemicontinuous.comp_continuous := UpperSemicontinuous.comp

end

/-! #### Addition -/


section

variable {Î¹ : Type*} {Î³ : Type*} [AddCommMonoid Î³] [LinearOrder Î³] [IsOrderedAddMonoid Î³]
  [TopologicalSpace Î³] [OrderTopology Î³]

/-- The sum of two upper semicontinuous functions is upper semicontinuous. Formulated with an
explicit continuity assumption on addition, for application to `EReal`. The unprimed version of
the lemma uses `[ContinuousAdd]`. -/
theorem UpperSemicontinuousWithinAt.add' {f g : Î± â†’ Î³} (hf : UpperSemicontinuousWithinAt f s x)
    (hg : UpperSemicontinuousWithinAt g s x)
    (hcont : ContinuousAt (fun p : Î³ Ã— Î³ => p.1 + p.2) (f x, g x)) :
    UpperSemicontinuousWithinAt (fun z => f z + g z) s x :=
  LowerSemicontinuousWithinAt.add' (Î³ := Î³áµ’áµˆ) hf hg hcont

/-- The sum of two upper semicontinuous functions is upper semicontinuous. Formulated with an
explicit continuity assumption on addition, for application to `EReal`. The unprimed version of
the lemma uses `[ContinuousAdd]`. -/
theorem UpperSemicontinuousAt.add' {f g : Î± â†’ Î³} (hf : UpperSemicontinuousAt f x)
    (hg : UpperSemicontinuousAt g x)
    (hcont : ContinuousAt (fun p : Î³ Ã— Î³ => p.1 + p.2) (f x, g x)) :
    UpperSemicontinuousAt (fun z => f z + g z) x := by
  simp_rw [â† upperSemicontinuousWithinAt_univ_iff] at *
  exact hf.add' hg hcont

/-- The sum of two upper semicontinuous functions is upper semicontinuous. Formulated with an
explicit continuity assumption on addition, for application to `EReal`. The unprimed version of
the lemma uses `[ContinuousAdd]`. -/
theorem UpperSemicontinuousOn.add' {f g : Î± â†’ Î³} (hf : UpperSemicontinuousOn f s)
    (hg : UpperSemicontinuousOn g s)
    (hcont : âˆ€ x âˆˆ s, ContinuousAt (fun p : Î³ Ã— Î³ => p.1 + p.2) (f x, g x)) :
    UpperSemicontinuousOn (fun z => f z + g z) s := fun x hx =>
  UpperSemicontinuousWithinAt.add' (hf x hx) (hg x hx) (hcont x hx)

/-- The sum of two upper semicontinuous functions is upper semicontinuous. Formulated with an
explicit continuity assumption on addition, for application to `EReal`. The unprimed version of
the lemma uses `[ContinuousAdd]`. -/
theorem UpperSemicontinuous.add' {f g : Î± â†’ Î³} (hf : UpperSemicontinuous f)
    (hg : UpperSemicontinuous g)
    (hcont : âˆ€ x, ContinuousAt (fun p : Î³ Ã— Î³ => p.1 + p.2) (f x, g x)) :
    UpperSemicontinuous fun z => f z + g z :=
  fun x => UpperSemicontinuousAt.add' (hf x) (hg x) (hcont x)

variable [ContinuousAdd Î³]

/-- The sum of two upper semicontinuous functions is upper semicontinuous. Formulated with
`[ContinuousAdd]`. The primed version of the lemma uses an explicit continuity assumption on
addition, for application to `EReal`. -/
theorem UpperSemicontinuousWithinAt.add {f g : Î± â†’ Î³} (hf : UpperSemicontinuousWithinAt f s x)
    (hg : UpperSemicontinuousWithinAt g s x) :
    UpperSemicontinuousWithinAt (fun z => f z + g z) s x :=
  hf.add' hg continuous_add.continuousAt

/-- The sum of two upper semicontinuous functions is upper semicontinuous. Formulated with
`[ContinuousAdd]`. The primed version of the lemma uses an explicit continuity assumption on
addition, for application to `EReal`. -/
theorem UpperSemicontinuousAt.add {f g : Î± â†’ Î³} (hf : UpperSemicontinuousAt f x)
    (hg : UpperSemicontinuousAt g x) : UpperSemicontinuousAt (fun z => f z + g z) x :=
  hf.add' hg continuous_add.continuousAt

/-- The sum of two upper semicontinuous functions is upper semicontinuous. Formulated with
`[ContinuousAdd]`. The primed version of the lemma uses an explicit continuity assumption on
addition, for application to `EReal`. -/
theorem UpperSemicontinuousOn.add {f g : Î± â†’ Î³} (hf : UpperSemicontinuousOn f s)
    (hg : UpperSemicontinuousOn g s) : UpperSemicontinuousOn (fun z => f z + g z) s :=
  hf.add' hg fun _x _hx => continuous_add.continuousAt

/-- The sum of two upper semicontinuous functions is upper semicontinuous. Formulated with
`[ContinuousAdd]`. The primed version of the lemma uses an explicit continuity assumption on
addition, for application to `EReal`. -/
theorem UpperSemicontinuous.add {f g : Î± â†’ Î³} (hf : UpperSemicontinuous f)
    (hg : UpperSemicontinuous g) : UpperSemicontinuous fun z => f z + g z :=
  hf.add' hg fun _x => continuous_add.continuousAt

theorem upperSemicontinuousWithinAt_sum {f : Î¹ â†’ Î± â†’ Î³} {a : Finset Î¹}
    (ha : âˆ€ i âˆˆ a, UpperSemicontinuousWithinAt (f i) s x) :
    UpperSemicontinuousWithinAt (fun z => âˆ‘ i âˆˆ a, f i z) s x :=
  lowerSemicontinuousWithinAt_sum (Î³ := Î³áµ’áµˆ) ha

theorem upperSemicontinuousAt_sum {f : Î¹ â†’ Î± â†’ Î³} {a : Finset Î¹}
    (ha : âˆ€ i âˆˆ a, UpperSemicontinuousAt (f i) x) :
    UpperSemicontinuousAt (fun z => âˆ‘ i âˆˆ a, f i z) x := by
  simp_rw [â† upperSemicontinuousWithinAt_univ_iff] at *
  exact upperSemicontinuousWithinAt_sum ha

theorem upperSemicontinuousOn_sum {f : Î¹ â†’ Î± â†’ Î³} {a : Finset Î¹}
    (ha : âˆ€ i âˆˆ a, UpperSemicontinuousOn (f i) s) :
    UpperSemicontinuousOn (fun z => âˆ‘ i âˆˆ a, f i z) s := fun x hx =>
  upperSemicontinuousWithinAt_sum fun i hi => ha i hi x hx

theorem upperSemicontinuous_sum {f : Î¹ â†’ Î± â†’ Î³} {a : Finset Î¹}
    (ha : âˆ€ i âˆˆ a, UpperSemicontinuous (f i)) : UpperSemicontinuous fun z => âˆ‘ i âˆˆ a, f i z :=
  fun x => upperSemicontinuousAt_sum fun i hi => ha i hi x

end

/-! #### Infimum -/

section

variable {Î± : Type*} {Î² : Type*} [TopologicalSpace Î±] [LinearOrder Î²]
    {f g : Î± â†’ Î²} {s : Set Î±} {a : Î±}

theorem UpperSemicontinuousWithinAt.inf
    (hf : UpperSemicontinuousWithinAt f s a) (hg : UpperSemicontinuousWithinAt g s a) :
    UpperSemicontinuousWithinAt (fun x â†¦ f x âŠ“ g x) s a :=
  LowerSemicontinuousWithinAt.sup (Î² := Î²áµ’áµˆ) hf hg

theorem UpperSemicontinuousAt.inf
    (hf : UpperSemicontinuousAt f a) (hg : UpperSemicontinuousAt g a) :
    UpperSemicontinuousAt (fun x â†¦ f x âŠ“ g x) a :=
  LowerSemicontinuousAt.sup (Î² := Î²áµ’áµˆ) hf hg

theorem UpperSemicontinuousOn.inf
    (hf : UpperSemicontinuousOn f s) (hg : UpperSemicontinuousOn g s) :
    UpperSemicontinuousOn (fun x â†¦ f x âŠ“ g x) s :=
  LowerSemicontinuousOn.sup (Î² := Î²áµ’áµˆ) hf hg

theorem UpperSemicontinuous.inf (hf : UpperSemicontinuous f) (hg : UpperSemicontinuous g) :
    UpperSemicontinuous (fun x â†¦ f x âŠ“ g x) :=
  LowerSemicontinuous.sup (Î² := Î²áµ’áµˆ) hf hg

theorem UpperSemicontinuousWithinAt.sup
    (hf : UpperSemicontinuousWithinAt f s a) (hg : UpperSemicontinuousWithinAt g s a) :
    UpperSemicontinuousWithinAt (fun x â†¦ f x âŠ” g x) s a :=
  LowerSemicontinuousWithinAt.inf (Î² := Î²áµ’áµˆ) hf hg

theorem UpperSemicontinuousAt.sup
    (hf : UpperSemicontinuousAt f a) (hg : UpperSemicontinuousAt g a) :
    UpperSemicontinuousAt (fun x â†¦ f x âŠ” g x) a :=
  LowerSemicontinuousAt.inf (Î² := Î²áµ’áµˆ) hf hg

theorem UpperSemicontinuousOn.sup
    (hf : UpperSemicontinuousOn f s) (hg : UpperSemicontinuousOn g s) :
    UpperSemicontinuousOn (fun x â†¦ f x âŠ” g x) s :=
  LowerSemicontinuousOn.inf (Î² := Î²áµ’áµˆ) hf hg

theorem UpperSemicontinuous.sup (hf : UpperSemicontinuous f) (hg : UpperSemicontinuous g) :
    UpperSemicontinuous fun x â†¦ f x âŠ” g x :=
  LowerSemicontinuous.inf (Î² := Î²áµ’áµˆ) hf hg


end

section

variable {Î¹ : Sort*} {Î´ Î´' : Type*} [CompleteLinearOrder Î´] [ConditionallyCompleteLinearOrder Î´']

theorem upperSemicontinuousWithinAt_ciInf {f : Î¹ â†’ Î± â†’ Î´'}
    (bdd : âˆ€á¶  y in ğ“[s] x, BddBelow (range fun i => f i y))
    (h : âˆ€ i, UpperSemicontinuousWithinAt (f i) s x) :
    UpperSemicontinuousWithinAt (fun x' => â¨… i, f i x') s x :=
  lowerSemicontinuousWithinAt_ciSup (Î´' := Î´'áµ’áµˆ) bdd h

theorem upperSemicontinuousWithinAt_iInf {f : Î¹ â†’ Î± â†’ Î´}
    (h : âˆ€ i, UpperSemicontinuousWithinAt (f i) s x) :
    UpperSemicontinuousWithinAt (fun x' => â¨… i, f i x') s x :=
  lowerSemicontinuousWithinAt_iSup (Î´ := Î´áµ’áµˆ) h

theorem upperSemicontinuousWithinAt_biInf {p : Î¹ â†’ Prop} {f : âˆ€ i, p i â†’ Î± â†’ Î´}
    (h : âˆ€ i hi, UpperSemicontinuousWithinAt (f i hi) s x) :
    UpperSemicontinuousWithinAt (fun x' => â¨… (i) (hi), f i hi x') s x :=
  upperSemicontinuousWithinAt_iInf fun i => upperSemicontinuousWithinAt_iInf fun hi => h i hi

theorem upperSemicontinuousAt_ciInf {f : Î¹ â†’ Î± â†’ Î´'}
    (bdd : âˆ€á¶  y in ğ“ x, BddBelow (range fun i => f i y)) (h : âˆ€ i, UpperSemicontinuousAt (f i) x) :
    UpperSemicontinuousAt (fun x' => â¨… i, f i x') x :=
  @lowerSemicontinuousAt_ciSup Î± _ x Î¹ Î´'áµ’áµˆ _ f bdd h

theorem upperSemicontinuousAt_iInf {f : Î¹ â†’ Î± â†’ Î´} (h : âˆ€ i, UpperSemicontinuousAt (f i) x) :
    UpperSemicontinuousAt (fun x' => â¨… i, f i x') x :=
  @lowerSemicontinuousAt_iSup Î± _ x Î¹ Î´áµ’áµˆ _ f h

theorem upperSemicontinuousAt_biInf {p : Î¹ â†’ Prop} {f : âˆ€ i, p i â†’ Î± â†’ Î´}
    (h : âˆ€ i hi, UpperSemicontinuousAt (f i hi) x) :
    UpperSemicontinuousAt (fun x' => â¨… (i) (hi), f i hi x') x :=
  upperSemicontinuousAt_iInf fun i => upperSemicontinuousAt_iInf fun hi => h i hi

theorem upperSemicontinuousOn_ciInf {f : Î¹ â†’ Î± â†’ Î´'}
    (bdd : âˆ€ x âˆˆ s, BddBelow (range fun i => f i x)) (h : âˆ€ i, UpperSemicontinuousOn (f i) s) :
    UpperSemicontinuousOn (fun x' => â¨… i, f i x') s := fun x hx =>
  upperSemicontinuousWithinAt_ciInf (eventually_nhdsWithin_of_forall bdd) fun i => h i x hx

theorem upperSemicontinuousOn_iInf {f : Î¹ â†’ Î± â†’ Î´} (h : âˆ€ i, UpperSemicontinuousOn (f i) s) :
    UpperSemicontinuousOn (fun x' => â¨… i, f i x') s := fun x hx =>
  upperSemicontinuousWithinAt_iInf fun i => h i x hx

theorem upperSemicontinuousOn_biInf {p : Î¹ â†’ Prop} {f : âˆ€ i, p i â†’ Î± â†’ Î´}
    (h : âˆ€ i hi, UpperSemicontinuousOn (f i hi) s) :
    UpperSemicontinuousOn (fun x' => â¨… (i) (hi), f i hi x') s :=
  upperSemicontinuousOn_iInf fun i => upperSemicontinuousOn_iInf fun hi => h i hi

theorem upperSemicontinuous_ciInf {f : Î¹ â†’ Î± â†’ Î´'} (bdd : âˆ€ x, BddBelow (range fun i => f i x))
    (h : âˆ€ i, UpperSemicontinuous (f i)) : UpperSemicontinuous fun x' => â¨… i, f i x' := fun x =>
  upperSemicontinuousAt_ciInf (Eventually.of_forall bdd) fun i => h i x

theorem upperSemicontinuous_iInf {f : Î¹ â†’ Î± â†’ Î´} (h : âˆ€ i, UpperSemicontinuous (f i)) :
    UpperSemicontinuous fun x' => â¨… i, f i x' := fun x => upperSemicontinuousAt_iInf fun i => h i x

theorem upperSemicontinuous_biInf {p : Î¹ â†’ Prop} {f : âˆ€ i, p i â†’ Î± â†’ Î´}
    (h : âˆ€ i hi, UpperSemicontinuous (f i hi)) :
    UpperSemicontinuous fun x' => â¨… (i) (hi), f i hi x' :=
  upperSemicontinuous_iInf fun i => upperSemicontinuous_iInf fun hi => h i hi

end

section

variable {Î³ : Type*} [LinearOrder Î³] [TopologicalSpace Î³] [OrderTopology Î³]

theorem continuousWithinAt_iff_lower_upperSemicontinuousWithinAt {f : Î± â†’ Î³} :
    ContinuousWithinAt f s x â†”
      LowerSemicontinuousWithinAt f s x âˆ§ UpperSemicontinuousWithinAt f s x := by
  refine âŸ¨fun h => âŸ¨h.lowerSemicontinuousWithinAt, h.upperSemicontinuousWithinAtâŸ©, ?_âŸ©
  rintro âŸ¨hâ‚, hâ‚‚âŸ©
  intro v hv
  simp only [Filter.mem_map]
  by_cases! Hl : âˆƒ l, l < f x
  Â· rcases exists_Ioc_subset_of_mem_nhds hv Hl with âŸ¨l, lfx, hlâŸ©
    by_cases! Hu : âˆƒ u, f x < u
    Â· rcases exists_Ico_subset_of_mem_nhds hv Hu with âŸ¨u, fxu, huâŸ©
      filter_upwards [hâ‚ l lfx, hâ‚‚ u fxu] with a lfa fau
      rcases le_or_gt (f a) (f x) with h | h
      Â· exact hl âŸ¨lfa, hâŸ©
      Â· exact hu âŸ¨le_of_lt h, fauâŸ©
    Â· filter_upwards [hâ‚ l lfx] with a lfa using hl âŸ¨lfa, Hu (f a)âŸ©
  Â· by_cases! Hu : âˆƒ u, f x < u
    Â· rcases exists_Ico_subset_of_mem_nhds hv Hu with âŸ¨u, fxu, huâŸ©
      filter_upwards [hâ‚‚ u fxu] with a lfa
      apply hu
      exact âŸ¨Hl (f a), lfaâŸ©
    Â· apply Filter.Eventually.of_forall
      intro a
      have : f a = f x := le_antisymm (Hu _) (Hl _)
      rw [this]
      exact mem_of_mem_nhds hv

theorem continuousAt_iff_lower_upperSemicontinuousAt {f : Î± â†’ Î³} :
    ContinuousAt f x â†” LowerSemicontinuousAt f x âˆ§ UpperSemicontinuousAt f x := by
  simp_rw [â† continuousWithinAt_univ, â† lowerSemicontinuousWithinAt_univ_iff, â†
    upperSemicontinuousWithinAt_univ_iff, continuousWithinAt_iff_lower_upperSemicontinuousWithinAt]

theorem continuousOn_iff_lower_upperSemicontinuousOn {f : Î± â†’ Î³} :
    ContinuousOn f s â†” LowerSemicontinuousOn f s âˆ§ UpperSemicontinuousOn f s := by
  simp only [ContinuousOn, continuousWithinAt_iff_lower_upperSemicontinuousWithinAt]
  exact
    âŸ¨fun H => âŸ¨fun x hx => (H x hx).1, fun x hx => (H x hx).2âŸ©, fun H x hx => âŸ¨H.1 x hx, H.2 x hxâŸ©âŸ©

theorem continuous_iff_lower_upperSemicontinuous {f : Î± â†’ Î³} :
    Continuous f â†” LowerSemicontinuous f âˆ§ UpperSemicontinuous f := by
  simp_rw [â† continuousOn_univ, continuousOn_iff_lower_upperSemicontinuousOn,
    lowerSemicontinuousOn_univ_iff, upperSemicontinuousOn_univ_iff]

end
