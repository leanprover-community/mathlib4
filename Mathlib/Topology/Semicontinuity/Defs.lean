/-
Copyright (c) 2021 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Antoine Chambert-Loir, Anatole Dedecker, Jireh Loreaux
-/
module

public import Mathlib.Topology.Defs.Induced
import Mathlib.Topology.ContinuousOn

/-!
# Semicontinuous maps

A function `f` from a topological space `Î±` to an ordered space `Î²` is *lower semicontinuous* at a
point `x` if, for any `y < f x`, for any `x'` close enough to `x`, one has `f x' > y`. In other
words, `f` can jump up, but it cannot jump down.

*Upper semicontinuous* functions are defined similarly. Upper and lower hemicontinuity (of
functions `f : Î± â†’ Set Î²`) are often defined in terms of sequential characterizations, but
here we take an equivalent approach. `f : Î± â†’ Set Î²` is *upper hemicontinuous* at `x` if for any
neighborhood of `f x`, `f x'` is included in this neighborhood for all `x'` close enough to `x`.

Of course, one can see a superficial similarity between upper semicontinuity and upper
hemicontinuity. In fact, we can unify all of upper and lower semicontinuity and also upper and
lower hemicontinuity under one umbrella, by considering a general relation `r : Î± â†’ Î² â†’ Prop` and
defining semicontinuity of this relation.

This file introduces these notions, and a basic API around them mimicking the API for continuous
functions.

## Main definitions and results

We introduce 4 generic definitions related to semicontinuity:
* `SemicontinuousWithinAt r s x`
* `SemicontinuousAt r x`
* `SemicontinuousOn r s`
* `Semicontinuous r`

We build a basic API using dot notation around these notions, and we prove that
* constant functions are semicontinuous;
* right composition with continuous functions preserves semicontinuity;

We also define lower and upper semicontinuity as abbreviations of these generic definitions
and transfer the generic results to these notions.

## References

* <https://en.wikipedia.org/wiki/Semi-continuity>
* <https://en.wikipedia.org/wiki/Hemicontinuity>

-/

@[expose] public section

open scoped Topology

open Set Function Filter

variable {Î± Î² Î³ : Type*} [TopologicalSpace Î±] [TopologicalSpace Î³]

/-! ## Main definitions -/

section Semicontinuous

/-- A relation `r : Î± â†’ Î² â†’ Prop` is semicontinuous within `s` at `x : Î±`, if whenever `r x y`
is true, it is also true for all `x'` sufficiently close to `x` within `s`.

This notion generalizes lower and upper semicontinuity of functions, as well as
lower and upper hemicontinuity of set-valued correspondences. -/
def SemicontinuousWithinAt (r : Î± â†’ Î² â†’ Prop) (s : Set Î±) (x : Î±) :=
  âˆ€ y, r x y â†’ âˆ€á¶  x' in ğ“[s] x, r x' y

/-- A relation `r : Î± â†’ Î² â†’ Prop` is semicontinuous on `s` if it is semicontinuous within `s` at
each `x âˆˆ s`. -/
def SemicontinuousOn (r : Î± â†’ Î² â†’ Prop) (s : Set Î±) :=
  âˆ€ x âˆˆ s, SemicontinuousWithinAt r s x

/-- A relation `r : Î± â†’ Î² â†’ Prop` is semicontinuous at `x : Î±`, if whenever `r x y`
is true, it is also true for all `x'` sufficiently close to `x`.

This notion generalizes lower and upper semicontinuity of functions, as well as
lower and upper hemicontinuity of set-valued correspondences. -/
def SemicontinuousAt (r : Î± â†’ Î² â†’ Prop) (x : Î±) : Prop :=
  âˆ€ y, r x y â†’ âˆ€á¶  x' in ğ“ x, r x' y

/-- A relation `r : Î± â†’ Î² â†’ Prop` is semicontinuous if it is semicontinuous within `s` at each
`x : Î±`. -/
def Semicontinuous (r : Î± â†’ Î² â†’ Prop) : Prop :=
  âˆ€ x, SemicontinuousAt r x

variable {r r' : Î± â†’ Î² â†’ Prop} {x : Î±} {s t : Set Î±}

theorem SemicontinuousWithinAt.mono (h : SemicontinuousWithinAt r s x) (hst : t âŠ† s) :
    SemicontinuousWithinAt r t x := fun y hy =>
  Filter.Eventually.filter_mono (nhdsWithin_mono _ hst) (h y hy)

theorem SemicontinuousWithinAt.congr_of_eventuallyEq {a : Î±}
    (h : SemicontinuousWithinAt r s a)
    (has : a âˆˆ s) (hfg : âˆ€á¶  x in ğ“[s] a, âˆ€ y, r x y â†” r' x y) :
    SemicontinuousWithinAt r' s a := by
  intro b hb
  simp_rw [â† propext_iff, â† funext_iff] at hfg
  rw [â† Filter.EventuallyEq.eq_of_nhdsWithin hfg has] at hb
  filter_upwards [hfg, h b hb] with x hx hxb
  exact hx â–¸ hxb

theorem semicontinuousWithinAt_univ_iff :
    SemicontinuousWithinAt r univ x â†” SemicontinuousAt r x := by
  simp [SemicontinuousWithinAt, SemicontinuousAt, nhdsWithin_univ]

theorem SemicontinuousAt.semicontinuousWithinAt (s : Set Î±)
    (h : SemicontinuousAt r x) : SemicontinuousWithinAt r s x := fun y hy =>
  Filter.Eventually.filter_mono nhdsWithin_le_nhds (h y hy)

theorem SemicontinuousOn.semicontinuousWithinAt (h : SemicontinuousOn r s)
    (hx : x âˆˆ s) : SemicontinuousWithinAt r s x :=
  h x hx

theorem SemicontinuousOn.mono (h : SemicontinuousOn r s) (hst : t âŠ† s) :
    SemicontinuousOn r t := fun x hx => (h x (hst hx)).mono hst

theorem semicontinuousOn_univ_iff : SemicontinuousOn r univ â†” Semicontinuous r := by
  simp [SemicontinuousOn, Semicontinuous, semicontinuousWithinAt_univ_iff]

@[simp] theorem semicontinuous_restrict_iff :
    Semicontinuous (s.restrict r) â†” SemicontinuousOn r s := by
  rw [SemicontinuousOn, Semicontinuous, SetCoe.forall]
  refine forallâ‚‚_congr fun a ha â†¦ forallâ‚‚_congr fun b _ â†¦ ?_
  simp only [nhdsWithin_eq_map_subtype_coe ha, eventually_map, restrict]

theorem Semicontinuous.semicontinuousAt (h : Semicontinuous r) (x : Î±) :
    SemicontinuousAt r x :=
  h x

theorem Semicontinuous.semicontinuousWithinAt (h : Semicontinuous r) (s : Set Î±)
    (x : Î±) : SemicontinuousWithinAt r s x :=
  (h x).semicontinuousWithinAt s

theorem Semicontinuous.semicontinuousOn (h : Semicontinuous r) (s : Set Î±) :
    SemicontinuousOn r s := fun x _hx => h.semicontinuousWithinAt s x

/-! #### Constants -/

theorem SemicontinuousWithinAt.const {f : Î² â†’ Prop} : SemicontinuousWithinAt (fun _x => f) s x :=
  fun _y hy => Filter.Eventually.of_forall fun _x => hy

theorem SemicontinuousAt.const {f : Î² â†’ Prop} : SemicontinuousAt (fun _x => f) x := fun _y hy =>
  Filter.Eventually.of_forall fun _x => hy

theorem SemicontinuousOn.const {f : Î² â†’ Prop} : SemicontinuousOn (fun _x => f) s := fun _x _hx =>
  SemicontinuousWithinAt.const

theorem Semicontinuous.const {f : Î² â†’ Prop} : Semicontinuous fun _x : Î± => f := fun _x =>
  SemicontinuousAt.const

/-! ### Precomposition with a continuous map -/

variable {x : Î³} {g : Î³ â†’ Î±} {s : Set Î³} {t : Set Î±}

lemma SemicontinuousWithinAt.comp (h : SemicontinuousWithinAt r t (g x))
    (hg : ContinuousWithinAt g s x) (hst : Set.MapsTo g s t) :
    SemicontinuousWithinAt (r âˆ˜ g) s x :=
  (hg.tendsto_nhdsWithin hst <| h Â· Â·)

lemma SemicontinuousOn.comp {r : Î± â†’ Î² â†’ Prop} {Î³ : Type*}
    [TopologicalSpace Î³] {g : Î³ â†’ Î±} {s : Set Î³} {t : Set Î±}
    (h : SemicontinuousOn r t) (hg : ContinuousOn g s)
    (hst : Set.MapsTo g s t) :
    SemicontinuousOn (r âˆ˜ g) s :=
  fun x hx â†¦ h (g x) (hst hx) |>.comp (hg x hx) hst

lemma SemicontinuousAt.comp {r : Î± â†’ Î² â†’ Prop} {Î³ : Type*} [TopologicalSpace Î³]
    {x : Î³} {g : Î³ â†’ Î±} (h : SemicontinuousAt r (g x)) (hg : ContinuousAt g x) :
    SemicontinuousAt (r âˆ˜ g) x :=
  (hg <| h Â· Â·)

lemma Semicontinuous.comp {r : Î± â†’ Î² â†’ Prop} {Î³ : Type*} [TopologicalSpace Î³]
    {g : Î³ â†’ Î±} (h : Semicontinuous r) (hg : Continuous g) :
    Semicontinuous (r âˆ˜ g) :=
  fun _ â†¦ (h.semicontinuousAt _).comp hg.continuousAt

end Semicontinuous

section Preorder

/-! ## Lower and Upper Semicontinuity -/

variable [Preorder Î²] {f g : Î± â†’ Î²} {x : Î±} {s t : Set Î±} {y z : Î²}

section Definitions

/-- A real function `f` is lower semicontinuous at `x` within a set `s` if, for any `Îµ > 0`, for all
`x'` close enough to `x` in `s`, then `f x'` is at least `f x - Îµ`. We formulate this in a general
preordered space, using an arbitrary `y < f x` instead of `f x - Îµ`. -/
abbrev LowerSemicontinuousWithinAt (f : Î± â†’ Î²) (s : Set Î±) (x : Î±) :=
  SemicontinuousWithinAt (f Â· > Â·) s x

/-- A real function `f` is lower semicontinuous on a set `s` if, for any `Îµ > 0`, for any `x âˆˆ s`,
for all `x'` close enough to `x` in `s`, then `f x'` is at least `f x - Îµ`. We formulate this in
a general preordered space, using an arbitrary `y < f x` instead of `f x - Îµ`. -/
abbrev LowerSemicontinuousOn (f : Î± â†’ Î²) (s : Set Î±) :=
  SemicontinuousOn (f Â· > Â·) s

/-- A real function `f` is lower semicontinuous at `x` if, for any `Îµ > 0`, for all `x'` close
enough to `x`, then `f x'` is at least `f x - Îµ`. We formulate this in a general preordered space,
using an arbitrary `y < f x` instead of `f x - Îµ`. -/
abbrev LowerSemicontinuousAt (f : Î± â†’ Î²) (x : Î±) :=
  SemicontinuousAt (f Â· > Â·) x

/-- A real function `f` is lower semicontinuous if, for any `Îµ > 0`, for any `x`, for all `x'` close
enough to `x`, then `f x'` is at least `f x - Îµ`. We formulate this in a general preordered space,
using an arbitrary `y < f x` instead of `f x - Îµ`. -/
abbrev LowerSemicontinuous (f : Î± â†’ Î²) :=
  Semicontinuous (f Â· > Â·)

/-- A real function `f` is upper semicontinuous at `x` within a set `s` if, for any `Îµ > 0`, for all
`x'` close enough to `x` in `s`, then `f x'` is at most `f x + Îµ`. We formulate this in a general
preordered space, using an arbitrary `y > f x` instead of `f x + Îµ`. -/
abbrev UpperSemicontinuousWithinAt (f : Î± â†’ Î²) (s : Set Î±) (x : Î±) :=
  SemicontinuousWithinAt (f Â· < Â·) s x

/-- A real function `f` is upper semicontinuous on a set `s` if, for any `Îµ > 0`, for any `x âˆˆ s`,
for all `x'` close enough to `x` in `s`, then `f x'` is at most `f x + Îµ`. We formulate this in a
general preordered space, using an arbitrary `y > f x` instead of `f x + Îµ`. -/
abbrev UpperSemicontinuousOn (f : Î± â†’ Î²) (s : Set Î±) :=
  SemicontinuousOn (f Â· < Â·) s

/-- A real function `f` is upper semicontinuous at `x` if, for any `Îµ > 0`, for all `x'` close
enough to `x`, then `f x'` is at most `f x + Îµ`. We formulate this in a general preordered space,
using an arbitrary `y > f x` instead of `f x + Îµ`. -/
abbrev UpperSemicontinuousAt (f : Î± â†’ Î²) (x : Î±) :=
  SemicontinuousAt (f Â· < Â·) x

/-- A real function `f` is upper semicontinuous if, for any `Îµ > 0`, for any `x`, for all `x'`
close enough to `x`, then `f x'` is at most `f x + Îµ`. We formulate this in a general preordered
space, using an arbitrary `y > f x` instead of `f x + Îµ`. -/
abbrev UpperSemicontinuous (f : Î± â†’ Î²) :=
  Semicontinuous (f Â· < Â·)

lemma lowerSemicontinuousWithinAt_iff {f : Î± â†’ Î²} {s : Set Î±} {x : Î±} :
    LowerSemicontinuousWithinAt f s x â†” âˆ€ y, y < f x â†’ âˆ€á¶  x' in ğ“[s] x, y < f x' :=
  Iff.rfl

lemma lowerSemicontinuousOn_iff {f : Î± â†’ Î²} {s : Set Î±} :
    LowerSemicontinuousOn f s â†” âˆ€ x âˆˆ s, LowerSemicontinuousWithinAt f s x :=
  Iff.rfl

lemma lowerSemicontinuousAt_iff {f : Î± â†’ Î²} {x : Î±} :
    LowerSemicontinuousAt f x â†” âˆ€ y, y < f x â†’ âˆ€á¶  x' in ğ“ x, y < f x' :=
  Iff.rfl

lemma lowerSemicontinuous_iff {f : Î± â†’ Î²} :
    LowerSemicontinuous f â†” âˆ€ x, LowerSemicontinuousAt f x :=
  Iff.rfl

lemma upperSemicontinuousWithinAt_iff {f : Î± â†’ Î²} {s : Set Î±} {x : Î±} :
    UpperSemicontinuousWithinAt f s x â†” âˆ€ y, f x < y â†’ âˆ€á¶  x' in ğ“[s] x, f x' < y :=
  Iff.rfl

lemma upperSemicontinuousOn_iff {f : Î± â†’ Î²} {s : Set Î±} :
    UpperSemicontinuousOn f s â†” âˆ€ x âˆˆ s, UpperSemicontinuousWithinAt f s x :=
  Iff.rfl

lemma upperSemicontinuousAt_iff {f : Î± â†’ Î²} {x : Î±} :
    UpperSemicontinuousAt f x â†” âˆ€ y, f x < y â†’ âˆ€á¶  x' in ğ“ x, f x' < y :=
  Iff.rfl

lemma upperSemicontinuous_iff {f : Î± â†’ Î²} :
    UpperSemicontinuous f â†” âˆ€ x, UpperSemicontinuousAt f x :=
  Iff.rfl

end Definitions

/-!
### Lower semicontinuous functions
-/

/-! #### Basic dot notation interface for lower semicontinuity -/

theorem LowerSemicontinuousWithinAt.mono (h : LowerSemicontinuousWithinAt f s x) (hst : t âŠ† s) :
    LowerSemicontinuousWithinAt f t x :=
  SemicontinuousWithinAt.mono h hst

theorem LowerSemicontinuousWithinAt.congr_of_eventuallyEq {a : Î±}
    (h : LowerSemicontinuousWithinAt f s a)
    (has : a âˆˆ s) (hfg : f =á¶ [ğ“[s] a] g) :
    LowerSemicontinuousWithinAt g s a :=
  SemicontinuousWithinAt.congr_of_eventuallyEq h has <| by
    filter_upwards [hfg] with x hx
    simp [hx]

theorem lowerSemicontinuousWithinAt_univ_iff :
    LowerSemicontinuousWithinAt f univ x â†” LowerSemicontinuousAt f x :=
  semicontinuousWithinAt_univ_iff

theorem LowerSemicontinuousAt.lowerSemicontinuousWithinAt (s : Set Î±)
    (h : LowerSemicontinuousAt f x) : LowerSemicontinuousWithinAt f s x :=
  h.semicontinuousWithinAt s

theorem LowerSemicontinuousOn.lowerSemicontinuousWithinAt (h : LowerSemicontinuousOn f s)
    (hx : x âˆˆ s) : LowerSemicontinuousWithinAt f s x :=
  h.semicontinuousWithinAt hx

theorem LowerSemicontinuousOn.mono (h : LowerSemicontinuousOn f s) (hst : t âŠ† s) :
    LowerSemicontinuousOn f t :=
  SemicontinuousOn.mono h hst

theorem lowerSemicontinuousOn_univ_iff : LowerSemicontinuousOn f univ â†” LowerSemicontinuous f :=
  semicontinuousOn_univ_iff

@[simp] theorem lowerSemicontinuous_restrict_iff :
    LowerSemicontinuous (s.restrict f) â†” LowerSemicontinuousOn f s :=
  semicontinuous_restrict_iff (r := (f Â· > Â·))

theorem LowerSemicontinuous.lowerSemicontinuousAt (h : LowerSemicontinuous f) (x : Î±) :
    LowerSemicontinuousAt f x :=
  h x

theorem LowerSemicontinuous.lowerSemicontinuousWithinAt (h : LowerSemicontinuous f) (s : Set Î±)
    (x : Î±) : LowerSemicontinuousWithinAt f s x :=
  (h x).semicontinuousWithinAt s

theorem LowerSemicontinuous.lowerSemicontinuousOn (h : LowerSemicontinuous f) (s : Set Î±) :
    LowerSemicontinuousOn f s :=
  h.semicontinuousOn s

/-! #### Constants -/

theorem lowerSemicontinuousWithinAt_const : LowerSemicontinuousWithinAt (fun _x => z) s x :=
  SemicontinuousWithinAt.const

theorem lowerSemicontinuousAt_const : LowerSemicontinuousAt (fun _x => z) x :=
  SemicontinuousAt.const

theorem lowerSemicontinuousOn_const : LowerSemicontinuousOn (fun _x => z) s :=
  SemicontinuousOn.const

theorem lowerSemicontinuous_const : LowerSemicontinuous fun _x : Î± => z :=
  Semicontinuous.const

/-! #### Composition -/
section

variable {g : Î³ â†’ Î±} {x : Î³} {t : Set Î³}

theorem LowerSemicontinuousWithinAt.comp
    (hf : LowerSemicontinuousWithinAt f s (g x)) (hg : ContinuousWithinAt g t x)
    (hg' : MapsTo g t s) :
    LowerSemicontinuousWithinAt (f âˆ˜ g) t x :=
  SemicontinuousWithinAt.comp hf hg hg'

theorem LowerSemicontinuousAt.comp
    (hf : LowerSemicontinuousAt f (g x)) (hg : ContinuousAt g x) :
    LowerSemicontinuousAt (f âˆ˜ g) x :=
  SemicontinuousAt.comp hf hg

theorem LowerSemicontinuousOn.comp
    (hf : LowerSemicontinuousOn f s) (hg : ContinuousOn g t) (hg' : MapsTo g t s) :
    LowerSemicontinuousOn (f âˆ˜ g) t :=
  SemicontinuousOn.comp hf hg hg'

theorem LowerSemicontinuous.comp
    (hf : LowerSemicontinuous f) (hg : Continuous g) : LowerSemicontinuous (f âˆ˜ g) :=
  Semicontinuous.comp hf hg

end

/-!
### Upper semicontinuous functions
-/


/-! #### Basic dot notation interface for upper semicontinuity -/


theorem UpperSemicontinuousWithinAt.mono (h : UpperSemicontinuousWithinAt f s x) (hst : t âŠ† s) :
    UpperSemicontinuousWithinAt f t x :=
  SemicontinuousWithinAt.mono h hst

theorem UpperSemicontinuousWithinAt.congr_of_eventuallyEq {a : Î±}
    (h : UpperSemicontinuousWithinAt f s a)
    (has : a âˆˆ s) (hfg : âˆ€á¶  x in nhdsWithin a s, f x = g x) :
    UpperSemicontinuousWithinAt g s a :=
  LowerSemicontinuousWithinAt.congr_of_eventuallyEq (Î² := Î²áµ’áµˆ) h has hfg

theorem upperSemicontinuousWithinAt_univ_iff :
    UpperSemicontinuousWithinAt f univ x â†” UpperSemicontinuousAt f x :=
  semicontinuousWithinAt_univ_iff

@[simp] theorem upperSemicontinuousOn_iff_restrict {s : Set Î±} :
    UpperSemicontinuous (s.restrict f) â†” UpperSemicontinuousOn f s :=
  lowerSemicontinuous_restrict_iff (Î² := Î²áµ’áµˆ)

theorem UpperSemicontinuousAt.upperSemicontinuousWithinAt (s : Set Î±)
    (h : UpperSemicontinuousAt f x) : UpperSemicontinuousWithinAt f s x :=
  h.semicontinuousWithinAt s

theorem UpperSemicontinuousOn.upperSemicontinuousWithinAt (h : UpperSemicontinuousOn f s)
    (hx : x âˆˆ s) : UpperSemicontinuousWithinAt f s x :=
  h x hx

theorem UpperSemicontinuousOn.mono (h : UpperSemicontinuousOn f s) (hst : t âŠ† s) :
    UpperSemicontinuousOn f t :=
  SemicontinuousOn.mono h hst

theorem upperSemicontinuousOn_univ_iff : UpperSemicontinuousOn f univ â†” UpperSemicontinuous f :=
  semicontinuousOn_univ_iff

theorem UpperSemicontinuous.upperSemicontinuousAt (h : UpperSemicontinuous f) (x : Î±) :
    UpperSemicontinuousAt f x :=
  h x

theorem UpperSemicontinuous.upperSemicontinuousWithinAt (h : UpperSemicontinuous f) (s : Set Î±)
    (x : Î±) : UpperSemicontinuousWithinAt f s x :=
  (h x).semicontinuousWithinAt s

theorem UpperSemicontinuous.upperSemicontinuousOn (h : UpperSemicontinuous f) (s : Set Î±) :
    UpperSemicontinuousOn f s :=
  h.semicontinuousOn s

/-! #### Constants -/

theorem upperSemicontinuousWithinAt_const : UpperSemicontinuousWithinAt (fun _x => z) s x :=
  SemicontinuousWithinAt.const

theorem upperSemicontinuousAt_const : UpperSemicontinuousAt (fun _x => z) x :=
  SemicontinuousAt.const

theorem upperSemicontinuousOn_const : UpperSemicontinuousOn (fun _x => z) s :=
  SemicontinuousOn.const

theorem upperSemicontinuous_const : UpperSemicontinuous fun _x : Î± => z :=
  Semicontinuous.const

/-! #### Composition -/

section

variable {g : Î³ â†’ Î±} {c : Î³} {t : Set Î³}

theorem UpperSemicontinuousWithinAt.comp
    (hf : UpperSemicontinuousWithinAt f s (g c)) (hg : ContinuousWithinAt g t c)
    (hg' : MapsTo g t s) :
    UpperSemicontinuousWithinAt (f âˆ˜ g) t c :=
  SemicontinuousWithinAt.comp (r := (f Â· < Â·)) hf hg hg' -- the elaboration aid is necessary.

theorem UpperSemicontinuousAt.comp
    (hf : UpperSemicontinuousAt f (g c)) (hg : ContinuousAt g c) :
    UpperSemicontinuousAt (f âˆ˜ g) c :=
  SemicontinuousAt.comp (r := (f Â· < Â·)) hf hg

theorem UpperSemicontinuousOn.comp
    (hf : UpperSemicontinuousOn f s) (hg : ContinuousOn g t) (hg' : MapsTo g t s) :
    UpperSemicontinuousOn (f âˆ˜ g) t :=
  SemicontinuousOn.comp (r := (f Â· < Â·)) hf hg hg'

theorem UpperSemicontinuous.comp
    (hf : UpperSemicontinuous f) (hg : Continuous g) : UpperSemicontinuous (f âˆ˜ g) :=
  Semicontinuous.comp (r := (f Â· < Â·)) hf hg

end

end Preorder

section Hemi

/-! ## Lower and Upper Semicontinuity -/

variable [TopologicalSpace Î²]

section Definitions

/-- A real function `f` is lower semicontinuous at `x` within a set `s` if, for any `Îµ > 0`, for all
`x'` close enough to `x` in `s`, then `f x'` is at least `f x - Îµ`. We formulate this in a general
preordered space, using an arbitrary `y < f x` instead of `f x - Îµ`. -/
abbrev LowerHemicontinuousWithinAt (f : Î± â†’ Set Î²) (s : Set Î±) (x : Î±) :=
  SemicontinuousWithinAt (fun x s â†¦ IsOpen s âˆ§ ((f x) âˆ© s).Nonempty) s x

/-- A real function `f` is lower semicontinuous on a set `s` if, for any `Îµ > 0`, for any `x âˆˆ s`,
for all `x'` close enough to `x` in `s`, then `f x'` is at least `f x - Îµ`. We formulate this in
a general preordered space, using an arbitrary `y < f x` instead of `f x - Îµ`. -/
abbrev LowerHemicontinuousOn (f : Î± â†’ Set Î²) (s : Set Î±) :=
  SemicontinuousOn (fun x s â†¦ IsOpen s âˆ§ ((f x) âˆ© s).Nonempty) s

/-- A real function `f` is lower semicontinuous at `x` if, for any `Îµ > 0`, for all `x'` close
enough to `x`, then `f x'` is at least `f x - Îµ`. We formulate this in a general preordered space,
using an arbitrary `y < f x` instead of `f x - Îµ`. -/
abbrev LowerHemicontinuousAt (f : Î± â†’ Set Î²) (x : Î±) :=
  SemicontinuousAt (fun x s â†¦ IsOpen s âˆ§ ((f x) âˆ© s).Nonempty) x

/-- A real function `f` is lower semicontinuous if, for any `Îµ > 0`, for any `x`, for all `x'` close
enough to `x`, then `f x'` is at least `f x - Îµ`. We formulate this in a general preordered space,
using an arbitrary `y < f x` instead of `f x - Îµ`. -/
abbrev LowerHemicontinuous (f : Î± â†’ Set Î²) :=
  Semicontinuous (fun x s â†¦ IsOpen s âˆ§ ((f x) âˆ© s).Nonempty)

open scoped Topology

/-- A real function `f` is upper semicontinuous at `x` within a set `s` if, for any `Îµ > 0`, for all
`x'` close enough to `x` in `s`, then `f x'` is at most `f x + Îµ`. We formulate this in a general
preordered space, using an arbitrary `y > f x` instead of `f x + Îµ`. -/
abbrev UpperHemicontinuousWithinAt (f : Î± â†’ Set Î²) (s : Set Î±) (x : Î±) :=
  SemicontinuousWithinAt (fun x s â†¦ s âˆˆ ğ“Ë¢ (f x)) s x

/-- A real function `f` is upper semicontinuous on a set `s` if, for any `Îµ > 0`, for any `x âˆˆ s`,
for all `x'` close enough to `x` in `s`, then `f x'` is at most `f x + Îµ`. We formulate this in a
general preordered space, using an arbitrary `y > f x` instead of `f x + Îµ`. -/
abbrev UpperHemicontinuousOn (f : Î± â†’ Set Î²) (s : Set Î±) :=
  SemicontinuousOn (fun x s â†¦ s âˆˆ ğ“Ë¢ (f x)) s

/-- A real function `f` is upper semicontinuous at `x` if, for any `Îµ > 0`, for all `x'` close
enough to `x`, then `f x'` is at most `f x + Îµ`. We formulate this in a general preordered space,
using an arbitrary `y > f x` instead of `f x + Îµ`. -/
abbrev UpperHemicontinuousAt (f : Î± â†’ Set Î²) (x : Î±) :=
  SemicontinuousAt (fun x s â†¦ s âˆˆ ğ“Ë¢ (f x)) x

/-- A real function `f` is upper semicontinuous if, for any `Îµ > 0`, for any `x`, for all `x'`
close enough to `x`, then `f x'` is at most `f x + Îµ`. We formulate this in a general preordered
space, using an arbitrary `y > f x` instead of `f x + Îµ`. -/
abbrev UpperHemicontinuous (f : Î± â†’ Set Î²) :=
  Semicontinuous (fun x s â†¦ s âˆˆ ğ“Ë¢ (f x))

lemma lowerHemicontinuousWithinAt_iff {f : Î± â†’ Set Î²} {s : Set Î±} {x : Î±} :
    LowerHemicontinuousWithinAt f s x â†”
      âˆ€ u, IsOpen u â†’ ((f x) âˆ© u).Nonempty â†’ âˆ€á¶  x' in ğ“[s] x, ((f x') âˆ© u).Nonempty := by
  simp +contextual [SemicontinuousWithinAt]

lemma lowerHemicontinuousOn_iff {f : Î± â†’ Set Î²} {s : Set Î±} :
    LowerHemicontinuousOn f s â†” âˆ€ x âˆˆ s, LowerHemicontinuousWithinAt f s x :=
  Iff.rfl

lemma lowerHemicontinuousAt_iff {f : Î± â†’ Set Î²} {x : Î±} :
    LowerHemicontinuousAt f x â†”
      âˆ€ u, IsOpen u â†’ ((f x) âˆ© u).Nonempty â†’ âˆ€á¶  x' in ğ“ x, ((f x') âˆ© u).Nonempty := by
  simp +contextual [SemicontinuousAt]

lemma lowerHemicontinuous_iff {f : Î± â†’ Set Î²} :
    LowerHemicontinuous f â†” âˆ€ x, LowerHemicontinuousAt f x :=
  Iff.rfl

lemma upperHemicontinuousWithinAt_iff {f : Î± â†’ Set Î²} {s : Set Î±} {x : Î±} :
    UpperHemicontinuousWithinAt f s x â†” âˆ€ t, t âˆˆ ğ“Ë¢ (f x) â†’ âˆ€á¶  x' in ğ“[s] x, t âˆˆ ğ“Ë¢ (f x') :=
  Iff.rfl

lemma upperHemicontinuousOn_iff {f : Î± â†’ Set Î²} {s : Set Î±} :
    UpperHemicontinuousOn f s â†” âˆ€ x âˆˆ s, UpperHemicontinuousWithinAt f s x :=
  Iff.rfl

lemma upperHemicontinuousAt_iff {f : Î± â†’ Set Î²} {x : Î±} :
    UpperHemicontinuousAt f x â†” âˆ€ t, t âˆˆ ğ“Ë¢ (f x) â†’ âˆ€á¶  x' in ğ“ x, t âˆˆ ğ“Ë¢ (f x') :=
  Iff.rfl

lemma upperHemicontinuous_iff {f : Î± â†’ Set Î²} :
    UpperHemicontinuous f â†” âˆ€ x, UpperHemicontinuousAt f x :=
  Iff.rfl

end Definitions

/-!
### Lower semicontinuous functions
-/

/-! #### Basic dot notation interface for lower semicontinuity -/

variable {f g : Î± â†’ Set Î²} {x : Î±} {s t : Set Î±} {y z : Set Î²}

theorem LowerHemicontinuousWithinAt.mono (h : LowerHemicontinuousWithinAt f s x) (hst : t âŠ† s) :
    LowerHemicontinuousWithinAt f t x :=
  SemicontinuousWithinAt.mono h hst

theorem LowerHemicontinuousWithinAt.congr_of_eventuallyEq {a : Î±}
    (h : LowerHemicontinuousWithinAt f s a)
    (has : a âˆˆ s) (hfg : f =á¶ [ğ“[s] a] g) :
    LowerHemicontinuousWithinAt g s a :=
  SemicontinuousWithinAt.congr_of_eventuallyEq h has <| by
    filter_upwards [hfg] with x hx
    simp [hx]

theorem lowerHemicontinuousWithinAt_univ_iff :
    LowerHemicontinuousWithinAt f univ x â†” LowerHemicontinuousAt f x :=
  semicontinuousWithinAt_univ_iff

theorem LowerHemicontinuousAt.lowerHemicontinuousWithinAt (s : Set Î±)
    (h : LowerHemicontinuousAt f x) : LowerHemicontinuousWithinAt f s x :=
  h.semicontinuousWithinAt s

theorem LowerHemicontinuousOn.lowerHemicontinuousWithinAt (h : LowerHemicontinuousOn f s)
    (hx : x âˆˆ s) : LowerHemicontinuousWithinAt f s x :=
  h.semicontinuousWithinAt hx

theorem LowerHemicontinuousOn.mono (h : LowerHemicontinuousOn f s) (hst : t âŠ† s) :
    LowerHemicontinuousOn f t :=
  SemicontinuousOn.mono h hst

theorem lowerHemicontinuousOn_univ_iff : LowerHemicontinuousOn f univ â†” LowerHemicontinuous f :=
  semicontinuousOn_univ_iff

@[simp] theorem lowerHemicontinuous_restrict_iff :
    LowerHemicontinuous (s.restrict f) â†” LowerHemicontinuousOn f s :=
  semicontinuous_restrict_iff (r := (fun x t â†¦ IsOpen t âˆ§ ((f x) âˆ© t).Nonempty))

theorem LowerHemicontinuous.lowerHemicontinuousAt (h : LowerHemicontinuous f) (x : Î±) :
    LowerHemicontinuousAt f x :=
  h x

theorem LowerHemicontinuous.lowerHemicontinuousWithinAt (h : LowerHemicontinuous f) (s : Set Î±)
    (x : Î±) : LowerHemicontinuousWithinAt f s x :=
  (h x).semicontinuousWithinAt s

theorem LowerHemicontinuous.lowerHemicontinuousOn (h : LowerHemicontinuous f) (s : Set Î±) :
    LowerHemicontinuousOn f s :=
  h.semicontinuousOn s

/-! #### Constants -/

theorem LowerHemicontinuousWithinAt.const : LowerHemicontinuousWithinAt (fun _x => z) s x :=
  SemicontinuousWithinAt.const

theorem LowerHemicontinuousAt.const : LowerHemicontinuousAt (fun _x => z) x :=
  SemicontinuousAt.const

theorem LowerHemicontinuousOn.const : LowerHemicontinuousOn (fun _x => z) s :=
  SemicontinuousOn.const

theorem LowerHemicontinuous.const : LowerHemicontinuous fun _x : Î± => z :=
  Semicontinuous.const

/-! #### Composition -/
section

variable {g : Î³ â†’ Î±} {x : Î³} {t : Set Î³}

theorem LowerHemicontinuousWithinAt.comp
    (hf : LowerHemicontinuousWithinAt f s (g x)) (hg : ContinuousWithinAt g t x)
    (hg' : MapsTo g t s) :
    LowerHemicontinuousWithinAt (f âˆ˜ g) t x :=
  SemicontinuousWithinAt.comp hf hg hg'

theorem LowerHemicontinuousAt.comp
    (hf : LowerHemicontinuousAt f (g x)) (hg : ContinuousAt g x) :
    LowerHemicontinuousAt (f âˆ˜ g) x :=
  SemicontinuousAt.comp hf hg

theorem LowerHemicontinuousOn.comp
    (hf : LowerHemicontinuousOn f s) (hg : ContinuousOn g t) (hg' : MapsTo g t s) :
    LowerHemicontinuousOn (f âˆ˜ g) t :=
  SemicontinuousOn.comp hf hg hg'

theorem LowerHemicontinuous.comp
    (hf : LowerHemicontinuous f) (hg : Continuous g) : LowerHemicontinuous (f âˆ˜ g) :=
  Semicontinuous.comp hf hg

end

/-!
### Upper semicontinuous functions
-/


/-! #### Basic dot notation interface for upper semicontinuity -/


theorem UpperHemicontinuousWithinAt.mono (h : UpperHemicontinuousWithinAt f s x) (hst : t âŠ† s) :
    UpperHemicontinuousWithinAt f t x :=
  SemicontinuousWithinAt.mono h hst

theorem UpperHemicontinuousWithinAt.congr_of_eventuallyEq {a : Î±}
    (h : UpperHemicontinuousWithinAt f s a)
    (has : a âˆˆ s) (hfg : âˆ€á¶  x in nhdsWithin a s, f x = g x) :
    UpperHemicontinuousWithinAt g s a :=
  SemicontinuousWithinAt.congr_of_eventuallyEq h has <| by
    filter_upwards [hfg] with x hx
    simp [hx]

theorem upperHemicontinuousWithinAt_univ_iff :
    UpperHemicontinuousWithinAt f univ x â†” UpperHemicontinuousAt f x :=
  semicontinuousWithinAt_univ_iff

@[simp] theorem upperHemicontinuousOn_iff_restrict {s : Set Î±} :
    UpperHemicontinuous (s.restrict f) â†” UpperHemicontinuousOn f s :=
  semicontinuous_restrict_iff (r := (fun x t â†¦ t âˆˆ ğ“Ë¢ (f x)))

theorem UpperHemicontinuousAt.upperHemicontinuousWithinAt (s : Set Î±)
    (h : UpperHemicontinuousAt f x) : UpperHemicontinuousWithinAt f s x :=
  h.semicontinuousWithinAt s

theorem UpperHemicontinuousOn.upperHemicontinuousWithinAt (h : UpperHemicontinuousOn f s)
    (hx : x âˆˆ s) : UpperHemicontinuousWithinAt f s x :=
  h x hx

theorem UpperHemicontinuousOn.mono (h : UpperHemicontinuousOn f s) (hst : t âŠ† s) :
    UpperHemicontinuousOn f t :=
  SemicontinuousOn.mono h hst

theorem upperHemicontinuousOn_univ_iff : UpperHemicontinuousOn f univ â†” UpperHemicontinuous f :=
  semicontinuousOn_univ_iff

theorem UpperHemicontinuous.upperHemicontinuousAt (h : UpperHemicontinuous f) (x : Î±) :
    UpperHemicontinuousAt f x :=
  h x

theorem UpperHemicontinuous.upperHemicontinuousWithinAt (h : UpperHemicontinuous f) (s : Set Î±)
    (x : Î±) : UpperHemicontinuousWithinAt f s x :=
  (h x).semicontinuousWithinAt s

theorem UpperHemicontinuous.upperHemicontinuousOn (h : UpperHemicontinuous f) (s : Set Î±) :
    UpperHemicontinuousOn f s :=
  h.semicontinuousOn s

/-! #### Constants -/

theorem UpperHemicontinuousWithinAt.const : UpperHemicontinuousWithinAt (fun _x => z) s x :=
  SemicontinuousWithinAt.const

theorem UpperHemicontinuousAt.const : UpperHemicontinuousAt (fun _x => z) x :=
  SemicontinuousAt.const

theorem UpperHemicontinuousOn.const : UpperHemicontinuousOn (fun _x => z) s :=
  SemicontinuousOn.const

theorem UpperHemicontinuous.const : UpperHemicontinuous fun _x : Î± => z :=
  Semicontinuous.const

/-! #### Composition -/

section

variable {g : Î³ â†’ Î±} {c : Î³} {t : Set Î³}

theorem UpperHemicontinuousWithinAt.comp
    (hf : UpperHemicontinuousWithinAt f s (g c)) (hg : ContinuousWithinAt g t c)
    (hg' : MapsTo g t s) :
    UpperHemicontinuousWithinAt (f âˆ˜ g) t c :=
  -- the elaboration aid is necessary.
  SemicontinuousWithinAt.comp (r := (fun x t â†¦ t âˆˆ ğ“Ë¢ (f x))) hf hg hg'

theorem UpperHemicontinuousAt.comp
    (hf : UpperHemicontinuousAt f (g c)) (hg : ContinuousAt g c) :
    UpperHemicontinuousAt (f âˆ˜ g) c :=
  SemicontinuousAt.comp (r := (fun x t â†¦ t âˆˆ ğ“Ë¢ (f x))) hf hg

theorem UpperHemicontinuousOn.comp
    (hf : UpperHemicontinuousOn f s) (hg : ContinuousOn g t) (hg' : MapsTo g t s) :
    UpperHemicontinuousOn (f âˆ˜ g) t :=
  SemicontinuousOn.comp (r := (fun x t â†¦ t âˆˆ ğ“Ë¢ (f x))) hf hg hg'

theorem UpperHemicontinuous.comp
    (hf : UpperHemicontinuous f) (hg : Continuous g) : UpperHemicontinuous (f âˆ˜ g) :=
  Semicontinuous.comp (r := (fun x t â†¦ t âˆˆ ğ“Ë¢ (f x))) hf hg

end

end Hemi
