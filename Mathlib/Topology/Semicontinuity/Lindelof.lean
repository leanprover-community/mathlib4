/-
Copyright (c) 2026 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
module

public import Mathlib.Topology.Semicontinuity.Basic
public import Mathlib.Topology.Compactness.Lindelof

/-!
# Enveloppes of Semicontinuous functions on Hereditarily LindelÃ¶f spaces

In this file, we show that, if `X` is a hereditarily LindelÃ¶f space and `ğ“•` is any family
of upper semicontinuous functions on `X`, then there is a countable subfamily `ğ“•'`
with the same infimum / lower enveloppe. Most importantly, this applies whenever `X` has a
`SecondCountableTopology`.

The codomain `E` of the functions is assumed to be linearly ordered, and to admit a countable
order-dense subset. In particular we do not assume that arbitrary infima exist in `E`, so our
result is of the form "if `IsGLB ğ“• s`, then there is a countable `ğ“•' âŠ† ğ“•` such that `IsGLB ğ“•' s`".

Of course we also provide the dual statements for lower semicontinuous functions.

## Implementation Notes

There is currently no way to state the hypothesis "`E` admits a countable order-dense subset"
which would be inferrable by typeclass inference. Instead, we assume
`[TopologicalSpace E] [OrderClosedTopology E] [DenselyOrdered E] [SeparableSpace E]`, and
use `Dense.exists_between` to show that a chosen countable dense subset is order-dense.

This is not completely satisfying because the hypotheses on `E` should be purely order-theoretic,
but in practice `E` is either `Real`, `NNReal`, `ENNReal` or `EReal`, all of which are already
equipped with the order topology.

## References

* [N. Bourbaki, *Topologie GÃ©nÃ©rale*, Chapitre IX, Appendice I][bourbaki1974] (this appendix does
  not seem to exist in the English translation)
-/

public section

open Set TopologicalSpace

variable {X E : Type*} [TopologicalSpace X] [HereditarilyLindelofSpace X] [LinearOrder E]
  [TopologicalSpace E] [OrderClosedTopology E] [DenselyOrdered E] [SeparableSpace E]
-- Note: we shouldn't really need a topology on `E`: we just want the conclusion of
-- `SeparableSpace` + `Dense.exists_between`.

/-- If a function `s : X â†’ E` can be written as the infimum of a family `ğ“•` of upper semicontinuous
functions then, assuming that `X` is hereditarily LindelÃ¶f (for example, second countable),
`s` can in fact be written as the infimum of some *countable* subfamily `ğ“•'`.

This is implication a) â‡’ b) in
[N. Bourbaki, *Topologie GÃ©nÃ©rale*, Chapitre IX, Appendice I, Proposition 3][bourbaki1974]

See the module docstring for a discussion of the assumptions on `E`. -/
theorem exists_countable_upperSemicontinuous_isGLB {s : X â†’ E} {ğ“• : Set (X â†’ E)}
    (hğ“•_cont : âˆ€ f âˆˆ ğ“•, UpperSemicontinuous f) (hğ“• : IsGLB ğ“• s) :
    âˆƒ ğ“•' âŠ† ğ“•, ğ“•'.Countable âˆ§ IsGLB ğ“•' s := by
  simp_rw [isGLB_pi] at *
  rcases exists_countable_dense E with âŸ¨D, D_count, D_denseâŸ©
  let U (f : X â†’ E) (d : E) : Set X := {x | f x < d}
  have U_open {f} (hf : f âˆˆ ğ“•) (d : E) : IsOpen (U f d) := hğ“•_cont f hf |>.isOpen_preimage d
  have (d : E) : {x | s x < d} = â‹ƒ f : ğ“•, U f d := by
    ext x
    simp [U, isGLB_lt_iff (hğ“• x)]
  have (d : E) : âˆƒ A âŠ† ğ“•, A.Countable âˆ§ {x | s x < d} = â‹ƒ f âˆˆ A, U f d := by
    simp_rw [this d]
    rcases eq_open_union_countable (fun f : ğ“• â†¦ U f d) (fun f â†¦ U_open f.2 d) with âŸ¨t, t_count, htâŸ©
    use (â†‘) '' t, image_val_subset, t_count.image _
    rw [â† ht, biUnion_image]
  choose A A_sub A_count hA using this
  set ğ“•' := â‹ƒ d âˆˆ D, A d
  have ğ“•'_sub : ğ“•' âŠ† ğ“• := iUnionâ‚‚_subset fun d _ â†¦ A_sub d
  use ğ“•', ğ“•'_sub, D_count.biUnion fun d _ â†¦ A_count d
  refine fun x â†¦ âŸ¨lowerBounds_mono_set (image_mono ğ“•'_sub) (hğ“• x).1, fun e he â†¦ ?_âŸ©
  by_contra! H
  rcases D_dense.exists_between H with âŸ¨d, d_mem, hdâŸ©
  obtain âŸ¨f, f_mem, hfâŸ© : âˆƒ f âˆˆ A d, f x < d := by
    have : x âˆˆ {y | s y < d} := hd.1
    simpa only [hA d, mem_iUnionâ‚‚, exists_prop, U, mem_setOf_eq] using this
  suffices e < e by simpa
  exact (he (mem_image_of_mem _ (mem_iUnionâ‚‚_of_mem d_mem f_mem))).trans_lt hf |>.trans hd.2

/-- If a function `s : X â†’ E` can be written as the supremum of a family `ğ“•` of lower semicontinuous
functions then, assuming that `X` is hereditarily LindelÃ¶f (for example, second countable),
`s` can in fact be written as the supremum of some *countable* subfamily `ğ“•'`.

This is implication a) â‡’ b) in
[N. Bourbaki, *Topologie GÃ©nÃ©rale*, Chapitre IX, Appendice I, Proposition 3][bourbaki1974]

See the module docstring for a discussion of the assumptions on `E`. -/
theorem exists_countable_lowerSemicontinuous_isLUB {s : X â†’ E} {ğ“• : Set (X â†’ E)}
    (hğ“•_cont : âˆ€ f âˆˆ ğ“•, LowerSemicontinuous f) (hğ“• : IsLUB ğ“• s) :
    âˆƒ ğ“•' âŠ† ğ“•, ğ“•'.Countable âˆ§ IsLUB ğ“•' s :=
  exists_countable_upperSemicontinuous_isGLB (E := Eáµ’áµˆ) hğ“•_cont hğ“•

end
