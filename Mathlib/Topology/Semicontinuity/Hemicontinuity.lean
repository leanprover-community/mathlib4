/-
Copyright (c) 2025 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
module

public import Mathlib.Topology.Semicontinuity.Defs
public import Mathlib.Topology.NhdsWithin
public import Mathlib.Topology.Separation.Regular
public import Mathlib.Topology.Defs.Sequences
import Mathlib.Topology.Sequences
import Mathlib.Topology.ContinuousOn

/-! # Hemicontinuity

This files provides basic facts about upper and lower hemicontinuity of correspondences
`f : Î± â†’ Set Î²`.
-/

public section

open Set Filter Topology

variable {Î± Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²]
variable {f g : Î± â†’ Set Î²} {s : Set Î±} {x : Î±}

/-! ### Basic facts -/

lemma upperHemicontinuousWithinAt_iff_forall_isOpen :
    UpperHemicontinuousWithinAt f s x â†” âˆ€ u, IsOpen u â†’ f x âŠ† u â†’ âˆ€á¶  x' in ğ“[s] x, f x' âŠ† u := by
  rw [upperHemicontinuousWithinAt_iff, hasBasis_nhdsSet _ |>.forall_iff ?mono]
  case mono => exact fun tâ‚ tâ‚‚ ht h â†¦ h.mp <| .of_forall fun x' hx' â†¦ mem_of_superset hx' ht
  simp only [and_imp]
  apply forallâ‚‚_congr
  simp +contextual [â† subset_interior_iff_mem_nhdsSet, IsOpen.interior_eq]

alias âŸ¨UpperHemicontinuousWithinAt.forall_isOpen, UpperHemicontinuousWithinAt.of_forall_isOpenâŸ© :=
  upperHemicontinuousWithinAt_iff_forall_isOpen

lemma upperHemicontinuousOn_iff_forall_isOpen :
    UpperHemicontinuousOn f s â†” âˆ€ x âˆˆ s, âˆ€ u, IsOpen u â†’ f x âŠ† u â†’ âˆ€á¶  x' in ğ“[s] x, f x' âŠ† u := by
  simp [upperHemicontinuousOn_iff, upperHemicontinuousWithinAt_iff_forall_isOpen]

alias âŸ¨UpperHemicontinuousOn.forall_isOpen, UpperHemicontinuousOn.of_forall_isOpenâŸ© :=
  upperHemicontinuousOn_iff_forall_isOpen

lemma upperHemicontinuousAt_iff_forall_isOpen :
    UpperHemicontinuousAt f x â†” âˆ€ u, IsOpen u â†’ f x âŠ† u â†’ âˆ€á¶  x' in ğ“ x, f x' âŠ† u := by
  simpa [upperHemicontinuousWithinAt_univ_iff] using
    upperHemicontinuousWithinAt_iff_forall_isOpen (s := Set.univ)

alias âŸ¨UpperHemicontinuousAt.forall_isOpen, UpperHemicontinuousAt.of_forall_isOpenâŸ© :=
  upperHemicontinuousAt_iff_forall_isOpen

lemma upperHemicontinuous_iff_forall_isOpen :
    UpperHemicontinuous f â†” âˆ€ x u, IsOpen u â†’ f x âŠ† u â†’ âˆ€á¶  x' in ğ“ x, f x' âŠ† u := by
  simp [upperHemicontinuous_iff, upperHemicontinuousAt_iff_forall_isOpen]

alias âŸ¨UpperHemicontinuous.forall_isOpen, UpperHemicontinuous.of_forall_isOpenâŸ© :=
  upperHemicontinuous_iff_forall_isOpen

/-! ### Characterization in terms of preimages of intervals of sets -/

lemma upperHemicontinuousWithinAt_iff_preimage_Iic :
    UpperHemicontinuousWithinAt f s x â†” âˆ€ u âˆˆ ğ“Ë¢ (f x), f â»Â¹' (Iic u) âˆˆ ğ“[s] x := by
  simp_rw [upperHemicontinuousWithinAt_iff]
  rw [hasBasis_nhdsSet (f x) |>.forall_iff ?hâ‚, hasBasis_nhdsSet (f x) |>.forall_iff ?hâ‚‚]
  case hâ‚‚ =>
    refine fun s t hst hs â†¦ Filter.mem_of_superset hs ?_
    gcongr
    exact hst
  case hâ‚ =>
    intro s t hst hs
    filter_upwards [hs] with x hx
    exact Filter.mem_of_superset hx hst
  refine forallâ‚‚_congr fun u âŸ¨hu, hfuâŸ© â†¦ ?_
  simp [hu.mem_nhdsSet, eventually_iff, Iic]

lemma upperHemicontinuousAt_iff_preimage_Iic :
    UpperHemicontinuousAt f x â†” âˆ€ u âˆˆ ğ“Ë¢ (f x), f â»Â¹' (Iic u) âˆˆ ğ“ x := by
  simpa [upperHemicontinuousWithinAt_univ_iff] using
    upperHemicontinuousWithinAt_iff_preimage_Iic (s := univ)

lemma upperHemicontinuousOn_iff_preimage_Iic :
    UpperHemicontinuousOn f s â†” âˆ€ x âˆˆ s, âˆ€ u âˆˆ ğ“Ë¢ (f x), f â»Â¹' (Iic u) âˆˆ ğ“[s] x := by
  simp [upperHemicontinuousOn_iff, upperHemicontinuousWithinAt_iff_preimage_Iic]

lemma upperHemicontinuous_iff_preimage_Iic :
    UpperHemicontinuous f â†” âˆ€ x, âˆ€ u âˆˆ ğ“Ë¢ (f x), f â»Â¹' (Iic u) âˆˆ ğ“ x := by
  simp [upperHemicontinuous_iff, upperHemicontinuousAt_iff_preimage_Iic]

/-- A correspondence `f : Î± â†’ Set Î²` is upper hemicontinuous if and only if its *upper inverse*
(i.e., `u : Set Î² â†¦ f â»Â¹' (Iic u)`, note that `f â»Â¹' (Iic u) = {x | f x âŠ† u}`) sends open sets
to open sets. -/
lemma upperHemicontinuous_iff_isOpen_preimage_Iic :
    UpperHemicontinuous f â†” âˆ€ u, IsOpen u â†’ IsOpen (f â»Â¹' (Iic u)) := by
  simp_rw [upperHemicontinuous_iff_preimage_Iic, isOpen_iff_mem_nhds (s := f â»Â¹' (Iic _))]
  conv =>
    enter [1, x]
    rw [hasBasis_nhdsSet (f x) |>.forall_iff <|
      fun s t hst hs â†¦ Filter.mem_of_superset hs <| by gcongr; exact hst]
  simp [forall_swap (Î± := Î±)]

/-- A correspondence `f : Î± â†’ Set Î²` is upper hemicontinuous if and only if its *lower inverse*
(i.e., `u : Set Î² â†¦ (f â»Â¹' (Iic uá¶œ))á¶œ`, note that `f â»Â¹' (Iic u) = {x | (f x âˆ© u).Nonempty}`)
sends closed sets to closed sets. -/
lemma upperHemicontinuous_iff_isClosed_compl_preimage_Iic_compl :
    UpperHemicontinuous f â†” âˆ€ u, IsClosed u â†’ IsClosed (f â»Â¹' (Iic uá¶œ))á¶œ := by
  conv_rhs =>
    rw [compl_surjective.forall]
    simp [â† isOpen_compl_iff]
  exact upperHemicontinuous_iff_isOpen_preimage_Iic

lemma isClosedMap_iff_upperHemicontinuous {f : Î± â†’ Î²} :
    IsClosedMap f â†” UpperHemicontinuous (f â»Â¹' {Â·}) := by
  rw [isClosedMap_iff_kernImage, upperHemicontinuous_iff_isOpen_preimage_Iic]
  aesop

/-- A correspondence `f : Î± â†’ Set Î²` is lower hemicontinuous if and only if its *lower inverse*
(i.e., `u : Set Î² â†¦ (f â»Â¹' (Iic uá¶œ))á¶œ`, note that `f â»Â¹' (Iic u) = {x | (f x âˆ© u).Nonempty}`)
sends open sets to open sets. -/
lemma lowerHemicontinuous_iff_isOpen_compl_preimage_Iic_compl :
    LowerHemicontinuous f â†” âˆ€ u, IsOpen u â†’ IsOpen (f â»Â¹' (Iic uá¶œ))á¶œ := by
  have (u : Set Î²) : (f â»Â¹' (Iic uá¶œ))á¶œ = {x | (f x âˆ© u).Nonempty} := by
    simp [Set.ext_iff, Iic, Set.mem_compl_iff, Set.not_subset, Set.Nonempty]
  simp_rw [lowerHemicontinuous_iff, lowerHemicontinuousAt_iff, this, isOpen_iff_mem_nhds,
    forall_swap (Î± := Î±), mem_setOf, Filter.Eventually]

/-- A correspondence `f : Î± â†’ Set Î²` is lower hemicontinuous if and only if its *upper inverse*
(i.e., `u : Set Î² â†¦ f â»Â¹' (Iic u)`, note that `f â»Â¹' (Iic u) = {x | f x âŠ† u}`) sends closed sets
to closed sets. -/
lemma lowerHemicontinuous_iff_isClosed_preimage_Iic :
    LowerHemicontinuous f â†” âˆ€ u, IsClosed u â†’ IsClosed (f â»Â¹' (Iic u)) := by
  conv_rhs =>
    rw [compl_surjective.forall]
    simp [â† isOpen_compl_iff]
  exact lowerHemicontinuous_iff_isOpen_compl_preimage_Iic_compl

lemma isOpenMap_iff_lowerHemicontinuous {f : Î± â†’ Î²} :
    IsOpenMap f â†” LowerHemicontinuous (f â»Â¹' {Â·}) := by
  rw [isOpenMap_iff_kernImage, lowerHemicontinuous_iff_isClosed_preimage_Iic]
  aesop

/-! ### Singleton maps -/

lemma upperHemicontinuous_singleton_id : UpperHemicontinuous ({Â·} : Î± â†’ Set Î±) := by
  simp [upperHemicontinuous_iff, upperHemicontinuousAt_iff]

@[simp]
lemma upperHemicontinuousWithinAt_singleton_iff {f : Î± â†’ Î²} {s : Set Î±} {x : Î±} :
    UpperHemicontinuousWithinAt ({f Â·}) s x â†” ContinuousWithinAt f s x := by
  refine âŸ¨?_, fun hf â†¦ upperHemicontinuous_singleton_id.upperHemicontinuousWithinAt _ _ |>.comp hf
    (mapsTo_image _ _)âŸ©
  simp only [upperHemicontinuousWithinAt_iff, nhdsSet_singleton, ContinuousWithinAt,
    tendsto_iff_forall_eventually_mem]
  intro h t ht
  filter_upwards [h t ht] with x
  exact mem_of_mem_nhds

@[simp]
lemma upperHemicontinuousAt_singleton_iff {f : Î± â†’ Î²} {x : Î±} :
    UpperHemicontinuousAt ({f Â·}) x â†” ContinuousAt f x := by
  simp [â† upperHemicontinuousWithinAt_univ_iff, continuousWithinAt_univ]

@[simp]
lemma upperHemicontinuousOn_singleton_iff {f : Î± â†’ Î²} {s : Set Î±} :
    UpperHemicontinuousOn ({f Â·}) s â†” ContinuousOn f s :=
  forallâ‚‚_congr <| fun _ _ â†¦ upperHemicontinuousWithinAt_singleton_iff

@[simp]
lemma upperHemicontinuous_singleton_iff {f : Î± â†’ Î²} :
    UpperHemicontinuous ({f Â·}) â†” Continuous f := by
  simp [â† upperHemicontinuousOn_univ_iff]


/-! ### Union and intersection, and post-composition with the preimage map -/

variable {Î± Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²]
variable {f g : Î± â†’ Set Î²} {s : Set Î±} {x : Î±}

/-- Pointwise unions of upper hemicontinuous maps are upper hemicontinuous. -/
lemma UpperHemicontinuousWithinAt.union (hf : UpperHemicontinuousWithinAt f s x)
    (hg : UpperHemicontinuousWithinAt g s x) :
    UpperHemicontinuousWithinAt (fun x â†¦ f x âˆª g x) s x := by
  rw [upperHemicontinuousWithinAt_iff] at hf hg âŠ¢
  aesop

/-- Pointwise unions of upper hemicontinuous maps are upper hemicontinuous. -/
lemma UpperHemicontinuousOn.union (hf : UpperHemicontinuousOn f s)
    (hg : UpperHemicontinuousOn g s) : UpperHemicontinuousOn (fun x â†¦ f x âˆª g x) s := by
  rw [upperHemicontinuousOn_iff] at hf hg âŠ¢
  exact fun x hx â†¦ (hf x hx).union (hg x hx)

/-- Pointwise unions of upper hemicontinuous maps are upper hemicontinuous. -/
lemma UpperHemicontinuousAt.union (hf : UpperHemicontinuousAt f x)
    (hg : UpperHemicontinuousAt g x) :
    UpperHemicontinuousAt (fun x â†¦ f x âˆª g x) x := by
  rw [â† upperHemicontinuousWithinAt_univ_iff] at hf hg âŠ¢
  exact hf.union hg

/-- Pointwise unions of upper hemicontinuous maps are upper hemicontinuous. -/
lemma UpperHemicontinuous.union (hf : UpperHemicontinuous f) (hg : UpperHemicontinuous g) :
    UpperHemicontinuous (fun x â†¦ f x âˆª g x) := by
  rw [upperHemicontinuous_iff] at hf hg âŠ¢
  exact fun x â†¦ (hf x).union (hg x)

/-- The pointwise intersection of an upper hemicontinuous function with a fixed closed set is
upper hemicontinuous. -/
lemma UpperHemicontinuousWithinAt.inter (hf : UpperHemicontinuousWithinAt f s x)
    {u : Set Î²} (hu : IsClosed u) :
    UpperHemicontinuousWithinAt (fun x â†¦ f x âˆ© u) s x := by
  rw [upperHemicontinuousWithinAt_iff_forall_isOpen] at hf âŠ¢
  intro t ht_open ht
  specialize hf (t âˆª uá¶œ) (ht_open.union hu.isOpen_compl) (by grind)
  grind

/-- The pointwise intersection of an upper hemicontinuous function with a fixed closed set is
upper hemicontinuous. -/
lemma UpperHemicontinuousOn.inter (hf : UpperHemicontinuousOn f s) {u : Set Î²} (hu : IsClosed u) :
    UpperHemicontinuousOn (fun x â†¦ f x âˆ© u) s := by
  rw [upperHemicontinuousOn_iff] at hf âŠ¢
  exact (hf Â· Â· |>.inter hu)

/-- The pointwise intersection of an upper hemicontinuous function with a fixed closed set is
upper hemicontinuous. -/
lemma UpperHemicontinuousAt.inter (hf : UpperHemicontinuousAt f x) {u : Set Î²} (hu : IsClosed u) :
    UpperHemicontinuousAt (fun x â†¦ f x âˆ© u) x := by
  rw [â† upperHemicontinuousWithinAt_univ_iff] at hf âŠ¢
  exact hf.inter hu

/-- The pointwise intersection of an upper hemicontinuous function with a fixed closed set is
upper hemicontinuous. -/
lemma UpperHemicontinuous.inter (hf : UpperHemicontinuous f) {u : Set Î²} (hu : IsClosed u) :
    UpperHemicontinuous (fun x â†¦ f x âˆ© u) := by
  rw [upperHemicontinuous_iff] at hf âŠ¢
  exact fun x â†¦ (hf x).inter hu

section Inducing

variable {Î³ : Type*} [TopologicalSpace Î³] {i : Î³ â†’ Î²}

/-- Post-composition with the preimage of an inducing function whose range is closed preserves
upper hemicontinuity. -/
lemma UpperHemicontinuousWithinAt.isInducing_comp (hf : UpperHemicontinuousWithinAt f s x)
    (hi : IsInducing i) (h_cl : IsClosed (range i)) :
    UpperHemicontinuousWithinAt (fun x â†¦ i â»Â¹' (f x)) s x := by
  refine .of_forall_isOpen fun u hu hifu â†¦ ?_
  obtain âŸ¨v, hv, rflâŸ© := hi.isOpen_iff.mp hu
  simp_rw [â† preimage_inter_range (s := f _), preimage_subset_preimage_iff inter_subset_right]
    at hifu âŠ¢
  exact hf.inter h_cl |>.forall_isOpen v hv hifu

/-- Post-composition with the preimage of an inducing function whose range is closed preserves
upper hemicontinuity. -/
lemma UpperHemicontinuousOn.isInducing_comp (hf : UpperHemicontinuousOn f s)
    (hi : IsInducing i) (h_cl : IsClosed (range i)) :
    UpperHemicontinuousOn (fun x â†¦ i â»Â¹' (f x)) s := by
  rw [upperHemicontinuousOn_iff] at hf âŠ¢
  exact fun x hx â†¦ (hf x hx).isInducing_comp hi h_cl

/-- Post-composition with the preimage of an inducing function whose range is closed preserves
upper hemicontinuity. -/
lemma UpperHemicontinuousAt.isInducing_comp (hf : UpperHemicontinuousAt f x)
    (hi : IsInducing i) (h_cl : IsClosed (range i)) :
    UpperHemicontinuousAt (fun x â†¦ i â»Â¹' (f x)) x := by
  simpa [upperHemicontinuousWithinAt_univ_iff] using
    hf.upperHemicontinuousWithinAt (s := Set.univ) |>.isInducing_comp hi h_cl

/-- Post-composition with the preimage of an inducing function whose range is closed preserves
upper hemicontinuity. -/
lemma UpperHemicontinuous.isInducing_comp (hf : UpperHemicontinuous f)
    (hi : IsInducing i) (h_cl : IsClosed (range i)) :
    UpperHemicontinuous (fun x â†¦ i â»Â¹' (f x)) := by
  rw [upperHemicontinuous_iff] at hf âŠ¢
  exact fun x â†¦ (hf x).isInducing_comp hi h_cl

end Inducing

/-- Upper hemicontinuous functions always have closed domain.

The more general fact is that if `f` is upper hemicontinuous at `xâ‚€` within `s`, and if
`xâ‚€` is a cluster point of `s âˆ© {x | (f x).Nonempty}`, then `(f xâ‚€).Nonempty`. -/
lemma UpperHemicontinuous.isClosed_domain {Î± Î² : Type*} [TopologicalSpace Î±]
    [TopologicalSpace Î²] {f : Î± â†’ Set Î²} (hf : UpperHemicontinuous f) :
    IsClosed {x | (f x).Nonempty} := by
  simp only [â† isOpen_compl_iff, compl_setOf, not_nonempty_iff_eq_empty, isOpen_iff_mem_nhds]
  intro x (hx : f x = âˆ…)
  simp_rw [upperHemicontinuous_iff, upperHemicontinuousAt_iff] at hf
  simpa [hx, empty_mem_iff_bot, nhdsSet_eq_bot_iff] using hf x âˆ…

/-! ### Sequential characterizations -/

/-- **Sequential characterization of upper hemicontinuity**:
A set-valued function `f : Î± â†’ Set Î²` is upper hemicontinuous at `xâ‚€ : Î±` if for every pair
of sequences `x : â„• â†’ Î±` and `y : â„• â†’ Î²` such that `x` tends to `xâ‚€` and `y n âˆˆ f (x n)` and
`y` tends to `yâ‚€ : Î²`, then `yâ‚€ âˆˆ f xâ‚€`. This requires that there is some (sequentially) compact
set containing all `f x'` for `x'` sufficiently close to `x`.

This is a partial converse of `UpperHemicontinuousAt.mem_of_tendsto`. -/
lemma UpperHemicontinuousAt.of_sequences {Î± Î² : Type*} [TopologicalSpace Î±]
    [TopologicalSpace Î²] {f : Î± â†’ Set Î²} {xâ‚€ : Î±} [(ğ“ xâ‚€).IsCountablyGenerated]
    {K : Set Î²} (hK : IsSeqCompact K) (hf : âˆ€á¶  x in ğ“ xâ‚€, f x âŠ† K)
    (h : âˆ€ x : â„• â†’ Î±, Tendsto x atTop (ğ“ xâ‚€) â†’
      âˆ€ y : â„• â†’ Î², (âˆ€ n, y n âˆˆ f (x n)) â†’ âˆ€ yâ‚€, Tendsto y atTop (ğ“ yâ‚€) â†’ yâ‚€ âˆˆ f xâ‚€) :
    UpperHemicontinuousAt f xâ‚€ := by
  refine .of_frequently fun t ht hft â†¦ ?_
  obtain âŸ¨x, hx, hfxâŸ© := exists_seq_forall_of_frequently hft
  choose y hy using hfx
  obtain âŸ¨yâ‚€, hyâ‚€, Ï†, hÏ†, hyÏ†âŸ© := hK.subseq_of_frequently_in (x := y) <| by
    refine Eventually.frequently ?_
    filter_upwards [hx hf] with n hn
    exact hn (hy n).1
  specialize h (x âˆ˜ Ï†) (hx.comp hÏ†.tendsto_atTop) (y âˆ˜ Ï†) (fun n â†¦ (hy _).1) _ hyÏ†
  exact âŸ¨yâ‚€, h, ht.closure_eq â–¸ mem_closure_of_tendsto hyÏ† <| .of_forall fun n â†¦ (hy _).2âŸ©

/-- **Sequential characterization of upper hemicontinuity**:
If `Î²` is a regular space and `f : Î± â†’ Set Î²` is upper hemicontinuous at `xâ‚€` and `f xâ‚€` is
closed, then for any sequences `x` and `y` (in `Î±` and `Î²`, respectively) tending to `xâ‚€` and `yâ‚€`,
respectively, if `y n âˆˆ f (x n)` frequently, then `yâ‚€ âˆˆ f xâ‚€`.

This is a partial converse of `UpperHemicontinuousAt.of_sequences`. -/
lemma UpperHemicontinuousAt.mem_of_tendsto {Î± Î² Î¹ : Type*} [TopologicalSpace Î±]
    [TopologicalSpace Î²] [RegularSpace Î²] {f : Î± â†’ Set Î²} {xâ‚€ : Î±} {l : Filter Î¹}
    (hf : UpperHemicontinuousAt f xâ‚€) (hf_closed : IsClosed (f xâ‚€))
    {x : Î¹ â†’ Î±} (hx : Tendsto x l (ğ“ xâ‚€))
    {y : Î¹ â†’ Î²} (hy : âˆƒá¶  n in l, y n âˆˆ f (x n)) {yâ‚€ : Î²} (hyâ‚€ : Tendsto y l (ğ“ yâ‚€)) :
    yâ‚€ âˆˆ f xâ‚€ := by
  by_contra
  obtain âŸ¨s, hs, t, ht, hstâŸ© := Filter.disjoint_iff.mp <| RegularSpace.regular hf_closed this
  suffices âˆƒá¶  n in l, y n âˆˆ s by
    apply this
    filter_upwards [hyâ‚€ ht] with n hn hyn
    exact hst.notMem_of_mem_left hyn hn
  apply hy.mp
  filter_upwards [hx (hf s hs)] with n hn hyn
  simp only [â† subset_interior_iff_mem_nhdsSet, preimage_setOf_eq, mem_setOf_eq] at hn
  exact interior_subset <| hn hyn
