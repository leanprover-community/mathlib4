/-
Copyright (c) 2023 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/

import Mathlib.Topology.Homeomorph
import Mathlib.Topology.Filter
import Mathlib.Order.Filter.Cofinite

/-!
# Proper maps between topological spaces
-/

open Filter Topology Function Set
open Prod (fst snd)

theorem IsClosedMap.image_closure_eq_of_continuous [TopologicalSpace X] [TopologicalSpace Y]
    {f : X â†’ Y} (f_closed : IsClosedMap f) (f_cont : Continuous f) (s : Set X) :
    f '' closure s = closure (f '' s) :=
  subset_antisymm f_cont.continuousOn.image_closure (f_closed.closure_image_subset s)

theorem IsClosedMap.map_lift'_closure_eq [TopologicalSpace X] [TopologicalSpace Y]
    {f : X â†’ Y} (f_closed : IsClosedMap f) (f_cont : Continuous f) (F : Filter X) :
    map f (F.lift' closure) = (map f F).lift' closure := by
  rw [map_lift'_eq2 (monotone_closure Y), map_lift'_eq (monotone_closure X)]
  congr
  ext s : 1
  exact f_closed.image_closure_eq_of_continuous f_cont s

theorem IsClosedMap.mapClusterPt_iff_lift'_closure [TopologicalSpace X] [TopologicalSpace Y]
    {F : Filter X} {f : X â†’ Y} (f_closed : IsClosedMap f) (f_cont : Continuous f) {y : Y} :
    MapClusterPt y F f â†” ((F.lift' closure) âŠ“ ğ“Ÿ (f â»Â¹' {y})).NeBot := by
  rw [MapClusterPt, clusterPt_iff_lift'_closure', â† f_closed.map_lift'_closure_eq f_cont,
      â† comap_principal, â† map_neBot_iff f, Filter.push_pull, principal_singleton]

-- Not needed anymore :tada:
lemma IsClosedMap.restrictPreimage [TopologicalSpace X] [TopologicalSpace Y]
    {f : X â†’ Y} (hcl : IsClosedMap f) (T : Set Y) :
    IsClosedMap (T.restrictPreimage f) := by
  rw [isClosedMap_iff_clusterPt] at hcl âŠ¢
  intro A âŸ¨y, hyTâŸ© hy
  rw [restrictPreimage, MapClusterPt, â† inducing_subtype_val.mapClusterPt_iff, MapClusterPt,
      map_map, MapsTo.restrict_commutes, â† map_map, â† MapClusterPt, map_principal] at hy
  rcases hcl _ y hy with âŸ¨x, hxy, hxâŸ©
  have hxT : f x âˆˆ T := hxy â–¸ hyT
  refine âŸ¨âŸ¨x, hxTâŸ©, Subtype.ext hxy, ?_âŸ©
  rwa [â† inducing_subtype_val.mapClusterPt_iff, MapClusterPt, map_principal]

variable [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z] [TopologicalSpace W]
  {f : X â†’ Y}

structure IsProperMap (f : X â†’ Y) extends Continuous f : Prop where
  clusterPt_of_mapClusterPt :
    âˆ€ â¦ƒâ„± : Filter Xâ¦„, âˆ€ â¦ƒy : Yâ¦„, MapClusterPt y â„± f â†’ âˆƒ x, f x = y âˆ§ ClusterPt x â„±

lemma IsProperMap.continuous (h : IsProperMap f) : Continuous f := h.toContinuous

lemma isProperMap_iff_clusterPt : IsProperMap f â†” Continuous f âˆ§
    âˆ€ â¦ƒâ„± : Filter Xâ¦„, âˆ€ â¦ƒy : Yâ¦„, MapClusterPt y â„± f â†’ âˆƒ x, f x = y âˆ§ ClusterPt x â„± :=
  âŸ¨fun h' â†¦ âŸ¨h'.continuous, h'.clusterPt_of_mapClusterPtâŸ©, fun âŸ¨h, h'âŸ© â†¦ âŸ¨h, h'âŸ©âŸ©

lemma Homeomorph.isProperMap (e : X â‰ƒâ‚œ Y) : IsProperMap e := by
  rw [isProperMap_iff_clusterPt]
  refine âŸ¨e.continuous, fun â„± y â†¦ ?_âŸ©
  simp_rw [MapClusterPt, ClusterPt, â† Filter.push_pull', map_neBot_iff, e.comap_nhds_eq,
    â† e.coe_toEquiv, â† e.eq_symm_apply, exists_eq_left]
  exact id

lemma isProperMap_id : IsProperMap (id : X â†’ X) := (Homeomorph.refl X).isProperMap

lemma IsProperMap.isClosedMap (h : IsProperMap f) : IsClosedMap f := by
  intro A hA
  rw [isClosed_iff_clusterPt] at hA âŠ¢
  intro y hy
  rw [â† map_principal] at hy
  rcases h.clusterPt_of_mapClusterPt hy with âŸ¨x, hxy, hxâŸ©
  exact âŸ¨x, hA x hx, hxyâŸ©

lemma isProperMap_iff_ultrafilter : IsProperMap f â†” Continuous f âˆ§
    âˆ€ â¦ƒğ’° : Ultrafilter Xâ¦„, âˆ€ â¦ƒy : Yâ¦„, Tendsto f ğ’° (ğ“ y) â†’ âˆƒ x, f x = y âˆ§ ğ’° â‰¤ ğ“ x := by
  rw [isProperMap_iff_clusterPt]
  refine and_congr_right (fun _ â†¦ ?_)
  constructor <;> intro H
  Â· intro ğ’° y (hY : (Ultrafilter.map f ğ’° : Filter Y) â‰¤ _)
    simp_rw [â† Ultrafilter.clusterPt_iff] at hY âŠ¢
    exact H hY
  Â· simp_rw [MapClusterPt, ClusterPt, â† Filter.push_pull', map_neBot_iff, â† exists_ultrafilter_iff,
      forall_exists_index]
    intro â„± y ğ’° hy
    rcases H (tendsto_iff_comap.mpr <| hy.trans inf_le_left) with âŸ¨x, hxy, hxâŸ©
    exact âŸ¨x, hxy, ğ’°, le_inf hx (hy.trans inf_le_right)âŸ©

lemma IsProperMap.ultrafilter_le_nhds_of_tendsto (h : IsProperMap f) â¦ƒğ’° : Ultrafilter Xâ¦„ â¦ƒy : Yâ¦„
    (hy : Tendsto f ğ’° (ğ“ y)) : âˆƒ x, f x = y âˆ§ ğ’° â‰¤ ğ“ x :=
  (isProperMap_iff_ultrafilter.mp h).2 hy

lemma IsProperMap.prod_map {g : Z â†’ W} (hf : IsProperMap f) (hg : IsProperMap g) :
    IsProperMap (Prod.map f g) := by
  simp_rw [isProperMap_iff_ultrafilter] at hf hg âŠ¢
  constructor
  Â· exact hf.1.prod_map hg.1
  Â· intro ğ’° âŸ¨y, wâŸ© hyw
    simp_rw [nhds_prod_eq, tendsto_prod_iff'] at hyw
    rcases hf.2 (show Tendsto f (Ultrafilter.map fst ğ’°) (ğ“ y) by simpa using hyw.1) with
      âŸ¨x, hxy, hxâŸ©
    rcases hg.2 (show Tendsto g (Ultrafilter.map snd ğ’°) (ğ“ w) by simpa using hyw.2) with
      âŸ¨z, hzw, hzâŸ©
    refine âŸ¨âŸ¨x, zâŸ©, Prod.ext hxy hzw, ?_âŸ©
    rw [nhds_prod_eq, le_prod]
    exact âŸ¨hx, hzâŸ©

lemma IsProperMap.pi_map {X Y : Î¹ â†’ Type _} [âˆ€ i, TopologicalSpace (X i)]
    [âˆ€ i, TopologicalSpace (Y i)] {f : (i : Î¹) â†’ X i â†’ Y i} (h : âˆ€ i, IsProperMap (f i)) :
    IsProperMap (fun (x : âˆ€ i, X i) i â†¦ f i (x i)) := by
  simp_rw [isProperMap_iff_ultrafilter] at h âŠ¢
  constructor
  Â· exact continuous_pi fun i â†¦ (h i).1.comp (continuous_apply i)
  Â· intro ğ’° y hy
    have : âˆ€ i, Tendsto (f i) (Ultrafilter.map (eval i) ğ’°) (ğ“ (y i)) :=
      by simpa [tendsto_pi_nhds] using hy
    choose x hxy hx using fun i â†¦ (h i).2 (this i)
    refine âŸ¨x, funext hxy, ?_âŸ©
    rwa [nhds_pi, le_pi]

theorem isProperMap_iff_isClosedMap_and_compact_fibers :
    IsProperMap f â†” Continuous f âˆ§ IsClosedMap f âˆ§ âˆ€ y, IsCompact (f â»Â¹' {y}) := by
  constructor <;> intro H
  -- Note: In Bourbaki, the direct implication is proved by going through universally closed maps.
  -- We could do the same (using a `TFAE` cycle) but the direct proof is nice enough already
  -- so we don't bother with that.
  Â· refine âŸ¨H.continuous, H.isClosedMap, fun y â†¦ ?_âŸ©
    rw [isCompact_iff_ultrafilter_le_nhds]
    intro ğ’° hğ’°
    rw [â† comap_principal, â† map_le_iff_le_comap, â† Ultrafilter.coe_map] at hğ’°
    rcases isCompact_singleton.ultrafilter_le_nhds _ hğ’° with âŸ¨y, rfl, hyâŸ©
    exact H.ultrafilter_le_nhds_of_tendsto hy
  Â· rw [isProperMap_iff_clusterPt]
    refine âŸ¨H.1, fun ğ“• y hy â†¦ ?_âŸ©
    rw [H.2.1.mapClusterPt_iff_lift'_closure H.1] at hy
    rcases H.2.2 y (f := Filter.lift' ğ“• closure âŠ“ ğ“Ÿ (f â»Â¹' {y})) inf_le_right with âŸ¨x, hxy, hxâŸ©
    refine âŸ¨x, hxy, ?_âŸ©
    rw [â† clusterPt_lift'_closure_iff]
    exact hx.mono inf_le_left

theorem IsProperMap.universally_closed (Z) [TopologicalSpace Z] (h : IsProperMap f) :
    IsClosedMap (Prod.map f id : X Ã— Z â†’ Y Ã— Z) :=
  (h.prod_map isProperMap_id).isClosedMap

theorem isProperMap_iff_isClosedMap_filter {X : Type u} {Y : Type v} [TopologicalSpace X]
    [TopologicalSpace Y] {f : X â†’ Y} :
    IsProperMap f â†” Continuous f âˆ§ IsClosedMap
      (Prod.map f id : X Ã— Filter X â†’ Y Ã— Filter X) := by
  constructor <;> intro H
  Â· exact âŸ¨H.continuous, H.universally_closed _âŸ©
  Â· rw [isProperMap_iff_ultrafilter]
    refine âŸ¨H.1, fun ğ’° y hy â†¦ ?_âŸ©
    let F : Set (X Ã— Filter X) := closure {xâ„± | xâ„±.2 = pure xâ„±.1}
    have := H.2 F isClosed_closure
    have : (y, â†‘ğ’°) âˆˆ Prod.map f id '' F :=
      this.mem_of_tendsto (hy.prod_mk_nhds (Filter.tendsto_pure_self (ğ’° : Filter X)))
        (eventually_of_forall fun x â†¦ âŸ¨âŸ¨x, pure xâŸ©, subset_closure rfl, rflâŸ©)
    rcases this with âŸ¨âŸ¨x, _âŸ©, hx, âŸ¨_, _âŸ©âŸ© -- I don't even understand how this works!
    refine âŸ¨x, rfl, fun U hU â†¦ Ultrafilter.compl_not_mem_iff.mp fun hUc â†¦ ?_âŸ©
    rw [mem_closure_iff_nhds] at hx
    rcases hx (U Ã—Ë¢ {ğ’¢ | Uá¶œ âˆˆ ğ’¢}) (prod_mem_nhds hU (isOpen_setOf_mem.mem_nhds hUc)) with
      âŸ¨âŸ¨y, ğ’¢âŸ©, âŸ¨âŸ¨hy : y âˆˆ U, hy' : Uá¶œ âˆˆ ğ’¢âŸ©, rfl : ğ’¢ = pure yâŸ©âŸ©
    exact hy' hy

theorem isProperMap_iff_universally_closed {X : Type u} {Y : Type v} [TopologicalSpace X]
    [TopologicalSpace Y] {f : X â†’ Y} :
    IsProperMap f â†” Continuous f âˆ§ âˆ€ (Z : Type u) [TopologicalSpace Z],
      IsClosedMap (Prod.map f id : X Ã— Z â†’ Y Ã— Z) := by
  constructor <;> intro H
  Â· exact âŸ¨H.continuous, fun Z â†¦ H.universally_closed _âŸ©
  Â· rw [isProperMap_iff_isClosedMap_filter]
    exact âŸ¨H.1, H.2 _âŸ©
