/-
Copyright (c) 2022 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn, Patrick Massot
-/
import Mathlib.Topology.Basic

#align_import topology.nhds_set from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Neighborhoods of a set

In this file we define the filter `ğ“Ë¢ s` or `nhdsSet s` consisting of all neighborhoods of a set
`s`.

## Main Properties

There are a couple different notions equivalent to `s âˆˆ ğ“Ë¢ t`:
* `s âŠ† interior t` using `subset_interior_iff_mem_nhdsSet`
* `âˆ€ x : Î±, x âˆˆ t â†’ s âˆˆ ğ“ x` using `mem_nhdsSet_iff_forall`
* `âˆƒ U : Set Î±, IsOpen U âˆ§ t âŠ† U âˆ§ U âŠ† s` using `mem_nhdsSet_iff_exists`

Furthermore, we have the following results:
* `monotone_nhdsSet`: `ğ“Ë¢` is monotone
* In Tâ‚-spaces, `ğ“Ë¢`is strictly monotone and hence injective:
  `strict_mono_nhdsSet`/`injective_nhdsSet`. These results are in `Mathlib.Topology.Separation`.

-/

open Set Filter Topology

variable {Î± Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²] {s t sâ‚ sâ‚‚ tâ‚ tâ‚‚ : Set Î±} {x : Î±}

/-- The filter of neighborhoods of a set in a topological space. -/
def nhdsSet (s : Set Î±) : Filter Î± :=
  sSup (nhds '' s)
#align nhds_set nhdsSet

@[inherit_doc] scoped[Topology] notation "ğ“Ë¢" => nhdsSet

theorem nhdsSet_diagonal (Î±) [TopologicalSpace (Î± Ã— Î±)] :
    ğ“Ë¢ (diagonal Î±) = â¨† (x : Î±), ğ“ (x, x) := by
  rw [nhdsSet, â† range_diag, â† range_comp]
  rfl
#align nhds_set_diagonal nhdsSet_diagonal

theorem mem_nhdsSet_iff_forall : s âˆˆ ğ“Ë¢ t â†” âˆ€ x : Î±, x âˆˆ t â†’ s âˆˆ ğ“ x := by
  simp_rw [nhdsSet, Filter.mem_sSup, ball_image_iff]
#align mem_nhds_set_iff_forall mem_nhdsSet_iff_forall

theorem bUnion_mem_nhdsSet {t : Î± â†’ Set Î±} (h : âˆ€ x âˆˆ s, t x âˆˆ ğ“ x) : (â‹ƒ x âˆˆ s, t x) âˆˆ ğ“Ë¢ s :=
  mem_nhdsSet_iff_forall.2 fun x hx => mem_of_superset (h x hx) <|
    subset_iUnionâ‚‚ (s := fun x _ => t x) x hx -- porting note: fails to find `s`
#align bUnion_mem_nhds_set bUnion_mem_nhdsSet

theorem subset_interior_iff_mem_nhdsSet : s âŠ† interior t â†” t âˆˆ ğ“Ë¢ s := by
  simp_rw [mem_nhdsSet_iff_forall, subset_interior_iff_nhds]
#align subset_interior_iff_mem_nhds_set subset_interior_iff_mem_nhdsSet

theorem disjoint_principal_nhdsSet : Disjoint (ğ“Ÿ s) (ğ“Ë¢ t) â†” Disjoint (closure s) t := by
  rw [disjoint_principal_left, â† subset_interior_iff_mem_nhdsSet, interior_compl,
    subset_compl_iff_disjoint_left]

theorem disjoint_nhdsSet_principal : Disjoint (ğ“Ë¢ s) (ğ“Ÿ t) â†” Disjoint s (closure t) := by
  rw [disjoint_comm, disjoint_principal_nhdsSet, disjoint_comm]

theorem mem_nhdsSet_iff_exists : s âˆˆ ğ“Ë¢ t â†” âˆƒ U : Set Î±, IsOpen U âˆ§ t âŠ† U âˆ§ U âŠ† s := by
  rw [â† subset_interior_iff_mem_nhdsSet, subset_interior_iff]
#align mem_nhds_set_iff_exists mem_nhdsSet_iff_exists

theorem hasBasis_nhdsSet (s : Set Î±) : (ğ“Ë¢ s).HasBasis (fun U => IsOpen U âˆ§ s âŠ† U) fun U => U :=
  âŸ¨fun t => by simp [mem_nhdsSet_iff_exists, and_assoc]âŸ©
#align has_basis_nhds_set hasBasis_nhdsSet

theorem IsOpen.mem_nhdsSet (hU : IsOpen s) : s âˆˆ ğ“Ë¢ t â†” t âŠ† s := by
  rw [â† subset_interior_iff_mem_nhdsSet, hU.interior_eq]
#align is_open.mem_nhds_set IsOpen.mem_nhdsSet

theorem principal_le_nhdsSet : ğ“Ÿ s â‰¤ ğ“Ë¢ s := fun _s hs =>
  (subset_interior_iff_mem_nhdsSet.mpr hs).trans interior_subset
#align principal_le_nhds_set principal_le_nhdsSet

@[simp]
theorem nhdsSet_eq_principal_iff : ğ“Ë¢ s = ğ“Ÿ s â†” IsOpen s := by
  rw [â† principal_le_nhdsSet.le_iff_eq, le_principal_iff, mem_nhdsSet_iff_forall,
    isOpen_iff_mem_nhds]
#align nhds_set_eq_principal_iff nhdsSet_eq_principal_iff

alias nhdsSet_eq_principal_iff â†” _ IsOpen.nhdsSet_eq
#align is_open.nhds_set_eq IsOpen.nhdsSet_eq

@[simp]
theorem nhdsSet_interior : ğ“Ë¢ (interior s) = ğ“Ÿ (interior s) :=
  isOpen_interior.nhdsSet_eq
#align nhds_set_interior nhdsSet_interior

@[simp]
theorem nhdsSet_singleton : ğ“Ë¢ {x} = ğ“ x := by
  ext
  rw [â† subset_interior_iff_mem_nhdsSet, â† mem_interior_iff_mem_nhds, singleton_subset_iff]
#align nhds_set_singleton nhdsSet_singleton

theorem mem_nhdsSet_interior : s âˆˆ ğ“Ë¢ (interior s) :=
  subset_interior_iff_mem_nhdsSet.mp Subset.rfl
#align mem_nhds_set_interior mem_nhdsSet_interior

@[simp]
theorem nhdsSet_empty : ğ“Ë¢ (âˆ… : Set Î±) = âŠ¥ := by rw [isOpen_empty.nhdsSet_eq, principal_empty]
#align nhds_set_empty nhdsSet_empty

theorem mem_nhdsSet_empty : s âˆˆ ğ“Ë¢ (âˆ… : Set Î±) := by simp
#align mem_nhds_set_empty mem_nhdsSet_empty

@[simp]
theorem nhdsSet_univ : ğ“Ë¢ (univ : Set Î±) = âŠ¤ := by rw [isOpen_univ.nhdsSet_eq, principal_univ]
#align nhds_set_univ nhdsSet_univ

@[mono]
theorem nhdsSet_mono (h : s âŠ† t) : ğ“Ë¢ s â‰¤ ğ“Ë¢ t :=
  sSup_le_sSup <| image_subset _ h
#align nhds_set_mono nhdsSet_mono

theorem monotone_nhdsSet : Monotone (ğ“Ë¢ : Set Î± â†’ Filter Î±) := fun _ _ => nhdsSet_mono
#align monotone_nhds_set monotone_nhdsSet

theorem nhds_le_nhdsSet (h : x âˆˆ s) : ğ“ x â‰¤ ğ“Ë¢ s :=
  le_sSup <| mem_image_of_mem _ h
#align nhds_le_nhds_set nhds_le_nhdsSet

@[simp]
theorem nhdsSet_union (s t : Set Î±) : ğ“Ë¢ (s âˆª t) = ğ“Ë¢ s âŠ” ğ“Ë¢ t := by
  simp only [nhdsSet, image_union, sSup_union]
#align nhds_set_union nhdsSet_union

theorem union_mem_nhdsSet (hâ‚ : sâ‚ âˆˆ ğ“Ë¢ tâ‚) (hâ‚‚ : sâ‚‚ âˆˆ ğ“Ë¢ tâ‚‚) : sâ‚ âˆª sâ‚‚ âˆˆ ğ“Ë¢ (tâ‚ âˆª tâ‚‚) := by
  rw [nhdsSet_union]
  exact union_mem_sup hâ‚ hâ‚‚
#align union_mem_nhds_set union_mem_nhdsSet

@[simp]
theorem nhdsSet_insert (x : Î±) (s : Set Î±) : ğ“Ë¢ (insert x s) = ğ“ x âŠ” ğ“Ë¢ s := by
  rw [insert_eq, nhdsSet_union, nhdsSet_singleton]

/-- Preimage of a set neighborhood of `t` under a continuous map `f` is a set neighborhood of `s`
provided that `f` maps `s` to `t`.  -/
theorem Continuous.tendsto_nhdsSet {f : Î± â†’ Î²} {t : Set Î²} (hf : Continuous f)
    (hst : MapsTo f s t) : Tendsto f (ğ“Ë¢ s) (ğ“Ë¢ t) :=
  ((hasBasis_nhdsSet s).tendsto_iff (hasBasis_nhdsSet t)).mpr fun U hU =>
    âŸ¨f â»Â¹' U, âŸ¨hU.1.preimage hf, hst.mono Subset.rfl hU.2âŸ©, fun _ => idâŸ©
#align continuous.tendsto_nhds_set Continuous.tendsto_nhdsSet
