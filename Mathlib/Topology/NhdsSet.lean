/-
Copyright (c) 2022 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn, Patrick Massot
-/
import Mathlib.Topology.Basic

#align_import topology.nhds_set from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Neighborhoods of a set

In this file we define the filter `ğ“Ë¢ s` or `nhdsSet s` consisting of all neighborhoods of a set
`s`.

## Main Properties

There are a couple different notions equivalent to `s âˆˆ ğ“Ë¢ t`:
* `s âŠ† interior t` using `subset_interior_iff_mem_nhdsSet`
* `âˆ€ x : X, x âˆˆ t â†’ s âˆˆ ğ“ x` using `mem_nhdsSet_iff_forall`
* `âˆƒ U : Set X, IsOpen U âˆ§ t âŠ† U âˆ§ U âŠ† s` using `mem_nhdsSet_iff_exists`

Furthermore, we have the following results:
* `monotone_nhdsSet`: `ğ“Ë¢` is monotone
* In Tâ‚-spaces, `ğ“Ë¢`is strictly monotone and hence injective:
  `strict_mono_nhdsSet`/`injective_nhdsSet`. These results are in `Mathlib.Topology.Separation`.

-/

open Set Filter Topology

variable {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] {f : Filter X}
  {s t sâ‚ sâ‚‚ tâ‚ tâ‚‚ : Set X} {x : X}

theorem nhdsSet_diagonal (X) [TopologicalSpace (X Ã— X)] :
    ğ“Ë¢ (diagonal X) = â¨† (x : X), ğ“ (x, x) := by
  rw [nhdsSet, â† range_diag, â† range_comp]
  rfl
#align nhds_set_diagonal nhdsSet_diagonal

theorem mem_nhdsSet_iff_forall : s âˆˆ ğ“Ë¢ t â†” âˆ€ x : X, x âˆˆ t â†’ s âˆˆ ğ“ x := by
  simp_rw [nhdsSet, Filter.mem_sSup, forall_mem_image]
#align mem_nhds_set_iff_forall mem_nhdsSet_iff_forall

lemma nhdsSet_le : ğ“Ë¢ s â‰¤ f â†” âˆ€ x âˆˆ s, ğ“ x â‰¤ f := by simp [nhdsSet]

theorem bUnion_mem_nhdsSet {t : X â†’ Set X} (h : âˆ€ x âˆˆ s, t x âˆˆ ğ“ x) : (â‹ƒ x âˆˆ s, t x) âˆˆ ğ“Ë¢ s :=
  mem_nhdsSet_iff_forall.2 fun x hx => mem_of_superset (h x hx) <|
    subset_iUnionâ‚‚ (s := fun x _ => t x) x hx -- Porting note: fails to find `s`
#align bUnion_mem_nhds_set bUnion_mem_nhdsSet

theorem subset_interior_iff_mem_nhdsSet : s âŠ† interior t â†” t âˆˆ ğ“Ë¢ s := by
  simp_rw [mem_nhdsSet_iff_forall, subset_interior_iff_nhds]
#align subset_interior_iff_mem_nhds_set subset_interior_iff_mem_nhdsSet

theorem disjoint_principal_nhdsSet : Disjoint (ğ“Ÿ s) (ğ“Ë¢ t) â†” Disjoint (closure s) t := by
  rw [disjoint_principal_left, â† subset_interior_iff_mem_nhdsSet, interior_compl,
    subset_compl_iff_disjoint_left]

theorem disjoint_nhdsSet_principal : Disjoint (ğ“Ë¢ s) (ğ“Ÿ t) â†” Disjoint s (closure t) := by
  rw [disjoint_comm, disjoint_principal_nhdsSet, disjoint_comm]

theorem mem_nhdsSet_iff_exists : s âˆˆ ğ“Ë¢ t â†” âˆƒ U : Set X, IsOpen U âˆ§ t âŠ† U âˆ§ U âŠ† s := by
  rw [â† subset_interior_iff_mem_nhdsSet, subset_interior_iff]
#align mem_nhds_set_iff_exists mem_nhdsSet_iff_exists

/-- A proposition is true on a set neighborhood of `s` iff it is true on a larger open set -/
theorem eventually_nhdsSet_iff_exists {p : X â†’ Prop} :
    (âˆ€á¶  x in ğ“Ë¢ s, p x) â†” âˆƒ t, IsOpen t âˆ§ s âŠ† t âˆ§ âˆ€ x, x âˆˆ t â†’ p x :=
  mem_nhdsSet_iff_exists

/-- A proposition is true on a set neighborhood of `s`
iff it is eventually true near each point in the set. -/
theorem eventually_nhdsSet_iff_forall {p : X â†’ Prop} :
    (âˆ€á¶  x in ğ“Ë¢ s, p x) â†” âˆ€ x, x âˆˆ s â†’ âˆ€á¶  y in ğ“ x, p y :=
  mem_nhdsSet_iff_forall

theorem hasBasis_nhdsSet (s : Set X) : (ğ“Ë¢ s).HasBasis (fun U => IsOpen U âˆ§ s âŠ† U) fun U => U :=
  âŸ¨fun t => by simp [mem_nhdsSet_iff_exists, and_assoc]âŸ©
#align has_basis_nhds_set hasBasis_nhdsSet

@[simp]
lemma lift'_nhdsSet_interior (s : Set X) : (ğ“Ë¢ s).lift' interior = ğ“Ë¢ s :=
  (hasBasis_nhdsSet s).lift'_interior_eq_self fun _ â†¦ And.left

lemma Filter.HasBasis.nhdsSet_interior {Î¹ : Sort*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set X} {t : Set X}
    (h : (ğ“Ë¢ t).HasBasis p s) : (ğ“Ë¢ t).HasBasis p (interior <| s Â·) :=
  lift'_nhdsSet_interior t â–¸ h.lift'_interior

theorem IsOpen.mem_nhdsSet (hU : IsOpen s) : s âˆˆ ğ“Ë¢ t â†” t âŠ† s := by
  rw [â† subset_interior_iff_mem_nhdsSet, hU.interior_eq]
#align is_open.mem_nhds_set IsOpen.mem_nhdsSet

/-- An open set belongs to its own set neighborhoods filter. -/
theorem IsOpen.mem_nhdsSet_self (ho : IsOpen s) : s âˆˆ ğ“Ë¢ s := ho.mem_nhdsSet.mpr Subset.rfl

theorem principal_le_nhdsSet : ğ“Ÿ s â‰¤ ğ“Ë¢ s := fun _s hs =>
  (subset_interior_iff_mem_nhdsSet.mpr hs).trans interior_subset
#align principal_le_nhds_set principal_le_nhdsSet

theorem subset_of_mem_nhdsSet (h : t âˆˆ ğ“Ë¢ s) : s âŠ† t := principal_le_nhdsSet h

theorem Filter.Eventually.self_of_nhdsSet {p : X â†’ Prop} (h : âˆ€á¶  x in ğ“Ë¢ s, p x) : âˆ€ x âˆˆ s, p x :=
  principal_le_nhdsSet h

nonrec theorem Filter.EventuallyEq.self_of_nhdsSet {f g : X â†’ Y} (h : f =á¶ [ğ“Ë¢ s] g) : EqOn f g s :=
  h.self_of_nhdsSet

@[simp]
theorem nhdsSet_eq_principal_iff : ğ“Ë¢ s = ğ“Ÿ s â†” IsOpen s := by
  rw [â† principal_le_nhdsSet.le_iff_eq, le_principal_iff, mem_nhdsSet_iff_forall,
    isOpen_iff_mem_nhds]
#align nhds_set_eq_principal_iff nhdsSet_eq_principal_iff

alias âŸ¨_, IsOpen.nhdsSet_eqâŸ© := nhdsSet_eq_principal_iff
#align is_open.nhds_set_eq IsOpen.nhdsSet_eq

@[simp]
theorem nhdsSet_interior : ğ“Ë¢ (interior s) = ğ“Ÿ (interior s) :=
  isOpen_interior.nhdsSet_eq
#align nhds_set_interior nhdsSet_interior

@[simp]
theorem nhdsSet_singleton : ğ“Ë¢ {x} = ğ“ x := by simp [nhdsSet]
#align nhds_set_singleton nhdsSet_singleton

theorem mem_nhdsSet_interior : s âˆˆ ğ“Ë¢ (interior s) :=
  subset_interior_iff_mem_nhdsSet.mp Subset.rfl
#align mem_nhds_set_interior mem_nhdsSet_interior

@[simp]
theorem nhdsSet_empty : ğ“Ë¢ (âˆ… : Set X) = âŠ¥ := by rw [isOpen_empty.nhdsSet_eq, principal_empty]
#align nhds_set_empty nhdsSet_empty

theorem mem_nhdsSet_empty : s âˆˆ ğ“Ë¢ (âˆ… : Set X) := by simp
#align mem_nhds_set_empty mem_nhdsSet_empty

@[simp]
theorem nhdsSet_univ : ğ“Ë¢ (univ : Set X) = âŠ¤ := by rw [isOpen_univ.nhdsSet_eq, principal_univ]
#align nhds_set_univ nhdsSet_univ

@[mono]
theorem nhdsSet_mono (h : s âŠ† t) : ğ“Ë¢ s â‰¤ ğ“Ë¢ t :=
  sSup_le_sSup <| image_subset _ h
#align nhds_set_mono nhdsSet_mono

theorem monotone_nhdsSet : Monotone (ğ“Ë¢ : Set X â†’ Filter X) := fun _ _ => nhdsSet_mono
#align monotone_nhds_set monotone_nhdsSet

theorem nhds_le_nhdsSet (h : x âˆˆ s) : ğ“ x â‰¤ ğ“Ë¢ s :=
  le_sSup <| mem_image_of_mem _ h
#align nhds_le_nhds_set nhds_le_nhdsSet

@[simp]
theorem nhdsSet_union (s t : Set X) : ğ“Ë¢ (s âˆª t) = ğ“Ë¢ s âŠ” ğ“Ë¢ t := by
  simp only [nhdsSet, image_union, sSup_union]
#align nhds_set_union nhdsSet_union

theorem union_mem_nhdsSet (hâ‚ : sâ‚ âˆˆ ğ“Ë¢ tâ‚) (hâ‚‚ : sâ‚‚ âˆˆ ğ“Ë¢ tâ‚‚) : sâ‚ âˆª sâ‚‚ âˆˆ ğ“Ë¢ (tâ‚ âˆª tâ‚‚) := by
  rw [nhdsSet_union]
  exact union_mem_sup hâ‚ hâ‚‚
#align union_mem_nhds_set union_mem_nhdsSet

@[simp]
theorem nhdsSet_insert (x : X) (s : Set X) : ğ“Ë¢ (insert x s) = ğ“ x âŠ” ğ“Ë¢ s := by
  rw [insert_eq, nhdsSet_union, nhdsSet_singleton]

/-- Preimage of a set neighborhood of `t` under a continuous map `f` is a set neighborhood of `s`
provided that `f` maps `s` to `t`.  -/
theorem Continuous.tendsto_nhdsSet {f : X â†’ Y} {t : Set Y} (hf : Continuous f)
    (hst : MapsTo f s t) : Tendsto f (ğ“Ë¢ s) (ğ“Ë¢ t) :=
  ((hasBasis_nhdsSet s).tendsto_iff (hasBasis_nhdsSet t)).mpr fun U hU =>
    âŸ¨f â»Â¹' U, âŸ¨hU.1.preimage hf, hst.mono Subset.rfl hU.2âŸ©, fun _ => idâŸ©
#align continuous.tendsto_nhds_set Continuous.tendsto_nhdsSet

lemma Continuous.tendsto_nhdsSet_nhds
    {y : Y} {f : X â†’ Y} (h : Continuous f) (h' : EqOn f (fun _ â†¦ y) s) :
    Tendsto f (ğ“Ë¢ s) (ğ“ y) := by
  rw [â† nhdsSet_singleton]
  exact h.tendsto_nhdsSet h'

/- This inequality cannot be improved to an equality. For instance,
if `X` has two elements and the coarse topology and `s` and `t` are distinct singletons then
`ğ“Ë¢ (s âˆ© t) = âŠ¥` while `ğ“Ë¢ s âŠ“ ğ“Ë¢ t = âŠ¤` and those are different. -/
theorem nhdsSet_inter_le (s t : Set X) : ğ“Ë¢ (s âˆ© t) â‰¤ ğ“Ë¢ s âŠ“ ğ“Ë¢ t :=
  (monotone_nhdsSet (X := X)).map_inf_le s t

variable (s) in
theorem IsClosed.nhdsSet_le_sup (h : IsClosed t) : ğ“Ë¢ s â‰¤ ğ“Ë¢ (s âˆ© t) âŠ” ğ“Ÿ (tá¶œ) :=
  calc
    ğ“Ë¢ s = ğ“Ë¢ (s âˆ© t âˆª s âˆ© tá¶œ) := by rw [Set.inter_union_compl s t]
    _ = ğ“Ë¢ (s âˆ© t) âŠ” ğ“Ë¢ (s âˆ© tá¶œ) := by rw [nhdsSet_union]
    _ â‰¤ ğ“Ë¢ (s âˆ© t) âŠ” ğ“Ë¢ (tá¶œ) := (sup_le_sup_left (monotone_nhdsSet (s.inter_subset_right (tá¶œ))) _)
    _ = ğ“Ë¢ (s âˆ© t) âŠ” ğ“Ÿ (tá¶œ) := by rw [h.isOpen_compl.nhdsSet_eq]

variable (s) in
theorem IsClosed.nhdsSet_le_sup' (h : IsClosed t) :
    ğ“Ë¢ s â‰¤ ğ“Ë¢ (t âˆ© s) âŠ” ğ“Ÿ (tá¶œ) := by rw [Set.inter_comm]; exact h.nhdsSet_le_sup s

theorem Filter.Eventually.eventually_nhdsSet {p : X â†’ Prop} (h : âˆ€á¶  y in ğ“Ë¢ s, p y) :
    âˆ€á¶  y in ğ“Ë¢ s, âˆ€á¶  x in ğ“ y, p x :=
  eventually_nhdsSet_iff_forall.mpr fun x x_in â†¦
    (eventually_nhdsSet_iff_forall.mp h x x_in).eventually_nhds

theorem Filter.Eventually.union_nhdsSet {p : X â†’ Prop} :
    (âˆ€á¶  x in ğ“Ë¢ (s âˆª t), p x) â†” (âˆ€á¶  x in ğ“Ë¢ s, p x) âˆ§ âˆ€á¶  x in ğ“Ë¢ t, p x := by
  rw [nhdsSet_union, eventually_sup]

theorem Filter.Eventually.union {p : X â†’ Prop} (hs : âˆ€á¶  x in ğ“Ë¢ s, p x) (ht : âˆ€á¶  x in ğ“Ë¢ t, p x) :
    âˆ€á¶  x in ğ“Ë¢ (s âˆª t), p x :=
  Filter.Eventually.union_nhdsSet.mpr âŸ¨hs, htâŸ©

theorem nhdsSet_iUnion {Î¹ : Sort*} (s : Î¹ â†’ Set X) : ğ“Ë¢ (â‹ƒ i, s i) = â¨† i, ğ“Ë¢ (s i) := by
  simp only [nhdsSet, image_iUnion, sSup_iUnion (Î² := Filter X)]

theorem eventually_nhdsSet_iUnionâ‚‚ {Î¹ : Sort*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set X} {P : X â†’ Prop} :
    (âˆ€á¶  x in ğ“Ë¢ (â‹ƒ (i) (_ : p i), s i), P x) â†” âˆ€ i, p i â†’ âˆ€á¶  x in ğ“Ë¢ (s i), P x := by
  simp only [nhdsSet_iUnion, eventually_iSup]

theorem eventually_nhdsSet_iUnion {Î¹ : Sort*} {s : Î¹ â†’ Set X} {P : X â†’ Prop} :
    (âˆ€á¶  x in ğ“Ë¢ (â‹ƒ i, s i), P x) â†” âˆ€ i, âˆ€á¶  x in ğ“Ë¢ (s i), P x := by
  simp only [nhdsSet_iUnion, eventually_iSup]
