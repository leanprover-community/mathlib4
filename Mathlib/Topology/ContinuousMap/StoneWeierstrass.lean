/-
Copyright (c) 2021 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Heather Macbeth
-/
import Mathlib.Algebra.Algebra.Subalgebra.Unitization
import Mathlib.Analysis.RCLike.Basic
import Mathlib.Topology.Algebra.StarSubalgebra
import Mathlib.Topology.ContinuousMap.ContinuousMapZero
import Mathlib.Topology.ContinuousMap.Weierstrass
import Mathlib.Topology.ContinuousMap.Lattice

/-!
# The Stone-Weierstrass theorem

If a subalgebra `A` of `C(X, â„)`, where `X` is a compact topological space,
separates points, then it is dense.

We argue as follows.

* In any subalgebra `A` of `C(X, â„)`, if `f âˆˆ A`, then `abs f âˆˆ A.topologicalClosure`.
  This follows from the Weierstrass approximation theorem on `[-â€–fâ€–, â€–fâ€–]` by
  approximating `abs` uniformly thereon by polynomials.
* This ensures that `A.topologicalClosure` is actually a sublattice:
  if it contains `f` and `g`, then it contains the pointwise supremum `f âŠ” g`
  and the pointwise infimum `f âŠ“ g`.
* Any nonempty sublattice `L` of `C(X, â„)` which separates points is dense,
  by a nice argument approximating a given `f` above and below using separating functions.
  For each `x y : X`, we pick a function `g x y âˆˆ L` so `g x y x = f x` and `g x y y = f y`.
  By continuity these functions remain close to `f` on small patches around `x` and `y`.
  We use compactness to identify a certain finitely indexed infimum of finitely indexed supremums
  which is then close to `f` everywhere, obtaining the desired approximation.
* Finally we put these pieces together. `L = A.topologicalClosure` is a nonempty sublattice
  which separates points since `A` does, and so is dense (in fact equal to `âŠ¤`).

We then prove the complex version for star subalgebras `A`, by separately approximating
the real and imaginary parts using the real subalgebra of real-valued functions in `A`
(which still separates points, by taking the norm-square of a separating function).

## Future work

Extend to cover the case of subalgebras of the continuous functions vanishing at infinity,
on non-compact spaces.

-/


noncomputable section

namespace ContinuousMap

variable {X : Type*} [TopologicalSpace X] [CompactSpace X]

open scoped Polynomial

/-- Turn a function `f : C(X, â„)` into a continuous map into `Set.Icc (-â€–fâ€–) (â€–fâ€–)`,
thereby explicitly attaching bounds.
-/
def attachBound (f : C(X, â„)) : C(X, Set.Icc (-â€–fâ€–) â€–fâ€–) where
  toFun x := âŸ¨f x, âŸ¨neg_norm_le_apply f x, apply_le_norm f xâŸ©âŸ©

@[simp]
theorem attachBound_apply_coe (f : C(X, â„)) (x : X) : ((attachBound f) x : â„) = f x :=
  rfl

theorem polynomial_comp_attachBound (A : Subalgebra â„ C(X, â„)) (f : A) (g : â„[X]) :
    (g.toContinuousMapOn (Set.Icc (-â€–fâ€–) â€–fâ€–)).comp (f : C(X, â„)).attachBound =
      Polynomial.aeval f g := by
  ext
  simp only [ContinuousMap.coe_comp, Function.comp_apply, ContinuousMap.attachBound_apply_coe,
    Polynomial.toContinuousMapOn_apply, Polynomial.aeval_subalgebra_coe,
    Polynomial.aeval_continuousMap_apply, Polynomial.toContinuousMap_apply]
  -- This used to be `rw`, but we need `erw` after https://github.com/leanprover/lean4/pull/2644
  erw [ContinuousMap.attachBound_apply_coe]

/-- Given a continuous function `f` in a subalgebra of `C(X, â„)`, postcomposing by a polynomial
gives another function in `A`.

This lemma proves something slightly more subtle than this:
we take `f`, and think of it as a function into the restricted target `Set.Icc (-â€–fâ€–) â€–fâ€–)`,
and then postcompose with a polynomial function on that interval.
This is in fact the same situation as above, and so also gives a function in `A`.
-/
theorem polynomial_comp_attachBound_mem (A : Subalgebra â„ C(X, â„)) (f : A) (g : â„[X]) :
    (g.toContinuousMapOn (Set.Icc (-â€–fâ€–) â€–fâ€–)).comp (f : C(X, â„)).attachBound âˆˆ A := by
  rw [polynomial_comp_attachBound]
  apply SetLike.coe_mem

theorem comp_attachBound_mem_closure (A : Subalgebra â„ C(X, â„)) (f : A)
    (p : C(Set.Icc (-â€–fâ€–) â€–fâ€–, â„)) : p.comp (attachBound (f : C(X, â„))) âˆˆ A.topologicalClosure := by
  -- `p` itself is in the closure of polynomials, by the Weierstrass theorem,
  have mem_closure : p âˆˆ (polynomialFunctions (Set.Icc (-â€–fâ€–) â€–fâ€–)).topologicalClosure :=
    continuousMap_mem_polynomialFunctions_closure _ _ p
  -- and so there are polynomials arbitrarily close.
  have frequently_mem_polynomials := mem_closure_iff_frequently.mp mem_closure
  -- To prove `p.comp (attachBound f)` is in the closure of `A`,
  -- we show there are elements of `A` arbitrarily close.
  apply mem_closure_iff_frequently.mpr
  -- To show that, we pull back the polynomials close to `p`,
  refine
    ((compRightContinuousMap â„ (attachBound (f : C(X, â„)))).continuousAt
            p).tendsto.frequently_map
      _ ?_ frequently_mem_polynomials
  -- but need to show that those pullbacks are actually in `A`.
  rintro _ âŸ¨g, âŸ¨-, rflâŸ©âŸ©
  simp only [SetLike.mem_coe, AlgHom.coe_toRingHom, compRightContinuousMap_apply,
    Polynomial.toContinuousMapOnAlgHom_apply]
  apply polynomial_comp_attachBound_mem

theorem abs_mem_subalgebra_closure (A : Subalgebra â„ C(X, â„)) (f : A) :
    |(f : C(X, â„))| âˆˆ A.topologicalClosure := by
  let f' := attachBound (f : C(X, â„))
  let abs : C(Set.Icc (-â€–fâ€–) â€–fâ€–, â„) := { toFun := fun x : Set.Icc (-â€–fâ€–) â€–fâ€– => |(x : â„)| }
  change abs.comp f' âˆˆ A.topologicalClosure
  apply comp_attachBound_mem_closure

theorem inf_mem_subalgebra_closure (A : Subalgebra â„ C(X, â„)) (f g : A) :
    (f : C(X, â„)) âŠ“ (g : C(X, â„)) âˆˆ A.topologicalClosure := by
  rw [inf_eq_half_smul_add_sub_abs_sub' â„]
  refine
    A.topologicalClosure.smul_mem
      (A.topologicalClosure.sub_mem
        (A.topologicalClosure.add_mem (A.le_topologicalClosure f.property)
          (A.le_topologicalClosure g.property))
        ?_)
      _
  exact mod_cast abs_mem_subalgebra_closure A _

theorem inf_mem_closed_subalgebra (A : Subalgebra â„ C(X, â„)) (h : IsClosed (A : Set C(X, â„)))
    (f g : A) : (f : C(X, â„)) âŠ“ (g : C(X, â„)) âˆˆ A := by
  convert inf_mem_subalgebra_closure A f g
  apply SetLike.ext'
  symm
  erw [closure_eq_iff_isClosed]
  exact h

theorem sup_mem_subalgebra_closure (A : Subalgebra â„ C(X, â„)) (f g : A) :
    (f : C(X, â„)) âŠ” (g : C(X, â„)) âˆˆ A.topologicalClosure := by
  rw [sup_eq_half_smul_add_add_abs_sub' â„]
  refine
    A.topologicalClosure.smul_mem
      (A.topologicalClosure.add_mem
        (A.topologicalClosure.add_mem (A.le_topologicalClosure f.property)
          (A.le_topologicalClosure g.property))
        ?_)
      _
  exact mod_cast abs_mem_subalgebra_closure A _

theorem sup_mem_closed_subalgebra (A : Subalgebra â„ C(X, â„)) (h : IsClosed (A : Set C(X, â„)))
    (f g : A) : (f : C(X, â„)) âŠ” (g : C(X, â„)) âˆˆ A := by
  convert sup_mem_subalgebra_closure A f g
  apply SetLike.ext'
  symm
  erw [closure_eq_iff_isClosed]
  exact h

open scoped Topology

-- Here's the fun part of Stone-Weierstrass!
theorem sublattice_closure_eq_top (L : Set C(X, â„)) (nA : L.Nonempty)
    (inf_mem : âˆ€áµ‰ (f âˆˆ L) (g âˆˆ L), f âŠ“ g âˆˆ L)
    (sup_mem : âˆ€áµ‰ (f âˆˆ L) (g âˆˆ L), f âŠ” g âˆˆ L) (sep : L.SeparatesPointsStrongly) :
    closure L = âŠ¤ := by
  -- We start by boiling down to a statement about close approximation.
  rw [eq_top_iff]
  rintro f -
  refine
    Filter.Frequently.mem_closure
      ((Filter.HasBasis.frequently_iff Metric.nhds_basis_ball).mpr fun Îµ pos => ?_)
  simp only [exists_prop, Metric.mem_ball]
  -- It will be helpful to assume `X` is nonempty later,
  -- so we get that out of the way here.
  by_cases nX : Nonempty X
  swap
  Â· exact âŸ¨nA.some, (dist_lt_iff pos).mpr fun x => False.elim (nX âŸ¨xâŸ©), nA.choose_specâŸ©
  /-
    The strategy now is to pick a family of continuous functions `g x y` in `A`
    with the property that `g x y x = f x` and `g x y y = f y`
    (this is immediate from `h : SeparatesPointsStrongly`)
    then use continuity to see that `g x y` is close to `f` near both `x` and `y`,
    and finally using compactness to produce the desired function `h`
    as a maximum over finitely many `x` of a minimum over finitely many `y` of the `g x y`.
    -/
  dsimp only [Set.SeparatesPointsStrongly] at sep
  choose g hg wâ‚ wâ‚‚ using sep f
  -- For each `x y`, we define `U x y` to be `{z | f z - Îµ < g x y z}`,
  -- and observe this is a neighbourhood of `y`.
  let U : X â†’ X â†’ Set X := fun x y => {z | f z - Îµ < g x y z}
  have U_nhd_y : âˆ€ x y, U x y âˆˆ ğ“ y := by
    intro x y
    refine IsOpen.mem_nhds ?_ ?_
    Â· apply isOpen_lt <;> continuity
    Â· rw [Set.mem_setOf_eq, wâ‚‚]
      exact sub_lt_self _ pos
  -- Fixing `x` for a moment, we have a family of functions `fun y â†¦ g x y`
  -- which on different patches (the `U x y`) are greater than `f z - Îµ`.
  -- Taking the supremum of these functions
  -- indexed by a finite collection of patches which cover `X`
  -- will give us an element of `A` that is globally greater than `f z - Îµ`
  -- and still equal to `f x` at `x`.
  -- Since `X` is compact, for every `x` there is some finset `ys t`
  -- so the union of the `U x y` for `y âˆˆ ys x` still covers everything.
  let ys : X â†’ Finset X := fun x => (CompactSpace.elim_nhds_subcover (U x) (U_nhd_y x)).choose
  let ys_w : âˆ€ x, â‹ƒ y âˆˆ ys x, U x y = âŠ¤ := fun x =>
    (CompactSpace.elim_nhds_subcover (U x) (U_nhd_y x)).choose_spec
  have ys_nonempty : âˆ€ x, (ys x).Nonempty := fun x =>
    Set.nonempty_of_union_eq_top_of_nonempty _ _ nX (ys_w x)
  -- Thus for each `x` we have the desired `h x : A` so `f z - Îµ < h x z` everywhere
  -- and `h x x = f x`.
  let h : X â†’ L := fun x =>
    âŸ¨(ys x).sup' (ys_nonempty x) fun y => (g x y : C(X, â„)),
      Finset.sup'_mem _ sup_mem _ _ _ fun y _ => hg x yâŸ©
  have lt_h : âˆ€ x z, f z - Îµ < (h x : X â†’ â„) z := by
    intro x z
    obtain âŸ¨y, ym, zmâŸ© := Set.exists_set_mem_of_union_eq_top _ _ (ys_w x) z
    dsimp
    simp only [Subtype.coe_mk, coe_sup', Finset.sup'_apply, Finset.lt_sup'_iff]
    exact âŸ¨y, ym, zmâŸ©
  have h_eq : âˆ€ x, (h x : X â†’ â„) x = f x := by intro x; simp [wâ‚]
  -- For each `x`, we define `W x` to be `{z | h x z < f z + Îµ}`,
  let W : X â†’ Set X := fun x => {z | (h x : X â†’ â„) z < f z + Îµ}
  -- This is still a neighbourhood of `x`.
  have W_nhd : âˆ€ x, W x âˆˆ ğ“ x := by
    intro x
    refine IsOpen.mem_nhds ?_ ?_
    Â· apply isOpen_lt <;> fun_prop
    Â· dsimp only [W, Set.mem_setOf_eq]
      rw [h_eq]
      exact lt_add_of_pos_right _ pos
  -- Since `X` is compact, there is some finset `ys t`
  -- so the union of the `W x` for `x âˆˆ xs` still covers everything.
  let xs : Finset X := (CompactSpace.elim_nhds_subcover W W_nhd).choose
  let xs_w : â‹ƒ x âˆˆ xs, W x = âŠ¤ := (CompactSpace.elim_nhds_subcover W W_nhd).choose_spec
  have xs_nonempty : xs.Nonempty := Set.nonempty_of_union_eq_top_of_nonempty _ _ nX xs_w
  -- Finally our candidate function is the infimum over `x âˆˆ xs` of the `h x`.
  -- This function is then globally less than `f z + Îµ`.
  let k : (L : Type _) :=
    âŸ¨xs.inf' xs_nonempty fun x => (h x : C(X, â„)),
      Finset.inf'_mem _ inf_mem _ _ _ fun x _ => (h x).2âŸ©
  refine âŸ¨k.1, ?_, k.2âŸ©
  -- We just need to verify the bound, which we do pointwise.
  rw [dist_lt_iff pos]
  intro z
  -- We rewrite into this particular form,
  -- so that simp lemmas about inequalities involving `Finset.inf'` can fire.
  rw [show âˆ€ a b Îµ : â„, dist a b < Îµ â†” a < b + Îµ âˆ§ b - Îµ < a by
        intros; simp only [â† Metric.mem_ball, Real.ball_eq_Ioo, Set.mem_Ioo, and_comm]]
  fconstructor
  Â· dsimp
    simp only [Finset.inf'_lt_iff, ContinuousMap.inf'_apply]
    exact Set.exists_set_mem_of_union_eq_top _ _ xs_w z
  Â· dsimp
    simp only [Finset.lt_inf'_iff, ContinuousMap.inf'_apply]
    rintro x -
    apply lt_h

/-- The **Stone-Weierstrass Approximation Theorem**,
that a subalgebra `A` of `C(X, â„)`, where `X` is a compact topological space,
is dense if it separates points.
-/
theorem subalgebra_topologicalClosure_eq_top_of_separatesPoints (A : Subalgebra â„ C(X, â„))
    (w : A.SeparatesPoints) : A.topologicalClosure = âŠ¤ := by
  -- The closure of `A` is closed under taking `sup` and `inf`,
  -- and separates points strongly (since `A` does),
  -- so we can apply `sublattice_closure_eq_top`.
  apply SetLike.ext'
  let L := A.topologicalClosure
  have n : Set.Nonempty (L : Set C(X, â„)) := âŸ¨(1 : C(X, â„)), A.le_topologicalClosure A.one_memâŸ©
  convert
    sublattice_closure_eq_top (L : Set C(X, â„)) n
      (fun f fm g gm => inf_mem_closed_subalgebra L A.isClosed_topologicalClosure âŸ¨f, fmâŸ© âŸ¨g, gmâŸ©)
      (fun f fm g gm => sup_mem_closed_subalgebra L A.isClosed_topologicalClosure âŸ¨f, fmâŸ© âŸ¨g, gmâŸ©)
      (Subalgebra.SeparatesPoints.strongly
        (Subalgebra.separatesPoints_monotone A.le_topologicalClosure w))
  simp [L]

/-- An alternative statement of the Stone-Weierstrass theorem.

If `A` is a subalgebra of `C(X, â„)` which separates points (and `X` is compact),
every real-valued continuous function on `X` is a uniform limit of elements of `A`.
-/
theorem continuousMap_mem_subalgebra_closure_of_separatesPoints (A : Subalgebra â„ C(X, â„))
    (w : A.SeparatesPoints) (f : C(X, â„)) : f âˆˆ A.topologicalClosure := by
  rw [subalgebra_topologicalClosure_eq_top_of_separatesPoints A w]
  simp

/-- An alternative statement of the Stone-Weierstrass theorem,
for those who like their epsilons.

If `A` is a subalgebra of `C(X, â„)` which separates points (and `X` is compact),
every real-valued continuous function on `X` is within any `Îµ > 0` of some element of `A`.
-/
theorem exists_mem_subalgebra_near_continuousMap_of_separatesPoints (A : Subalgebra â„ C(X, â„))
    (w : A.SeparatesPoints) (f : C(X, â„)) (Îµ : â„) (pos : 0 < Îµ) :
    âˆƒ g : A, â€–(g : C(X, â„)) - fâ€– < Îµ := by
  have w :=
    mem_closure_iff_frequently.mp (continuousMap_mem_subalgebra_closure_of_separatesPoints A w f)
  rw [Metric.nhds_basis_ball.frequently_iff] at w
  obtain âŸ¨g, H, mâŸ© := w Îµ pos
  rw [Metric.mem_ball, dist_eq_norm] at H
  exact âŸ¨âŸ¨g, mâŸ©, HâŸ©

/-- An alternative statement of the Stone-Weierstrass theorem,
for those who like their epsilons and don't like bundled continuous functions.

If `A` is a subalgebra of `C(X, â„)` which separates points (and `X` is compact),
every real-valued continuous function on `X` is within any `Îµ > 0` of some element of `A`.
-/
theorem exists_mem_subalgebra_near_continuous_of_separatesPoints (A : Subalgebra â„ C(X, â„))
    (w : A.SeparatesPoints) (f : X â†’ â„) (c : Continuous f) (Îµ : â„) (pos : 0 < Îµ) :
    âˆƒ g : A, âˆ€ x, â€–(g : X â†’ â„) x - f xâ€– < Îµ := by
  obtain âŸ¨g, bâŸ© := exists_mem_subalgebra_near_continuousMap_of_separatesPoints A w âŸ¨f, câŸ© Îµ pos
  use g
  rwa [norm_lt_iff _ pos] at b

end ContinuousMap

section RCLike

open RCLike

-- Redefine `X`, since for the next lemma it need not be compact
variable {ğ•œ : Type*} {X : Type*} [RCLike ğ•œ] [TopologicalSpace X]

open ContinuousMap

/- a post-port refactor eliminated `conjInvariantSubalgebra`, which was only used to
state and prove the Stone-Weierstrass theorem, in favor of using `StarSubalgebra`s,
which didn't exist at the time Stone-Weierstrass was written. -/


/-- If a star subalgebra of `C(X, ğ•œ)` separates points, then the real subalgebra
of its purely real-valued elements also separates points. -/
theorem Subalgebra.SeparatesPoints.rclike_to_real {A : StarSubalgebra ğ•œ C(X, ğ•œ)}
    (hA : A.SeparatesPoints) :
      ((A.restrictScalars â„).comap
        (ofRealAm.compLeftContinuous â„ continuous_ofReal)).SeparatesPoints := by
  intro xâ‚ xâ‚‚ hx
  -- Let `f` in the subalgebra `A` separate the points `xâ‚`, `xâ‚‚`
  obtain âŸ¨_, âŸ¨f, hfA, rflâŸ©, hfâŸ© := hA hx
  let F : C(X, ğ•œ) := f - const _ (f xâ‚‚)
  -- Subtract the constant `f xâ‚‚` from `f`; this is still an element of the subalgebra
  have hFA : F âˆˆ A := by
    refine A.sub_mem hfA (@Eq.subst _ (Â· âˆˆ A) _ _ ?_ <| A.smul_mem A.one_mem <| f xâ‚‚)
    ext1
    simp only [coe_smul, coe_one, smul_apply, one_apply, Algebra.id.smul_eq_mul, mul_one,
      const_apply]
  -- Consider now the function `fun x â†¦ |f x - f xâ‚‚| ^ 2`
  refine âŸ¨_, âŸ¨âŸ¨(â€–F Â·â€– ^ 2), by continuityâŸ©, ?_, rflâŸ©, ?_âŸ©
  Â· -- This is also an element of the subalgebra, and takes only real values
    rw [SetLike.mem_coe, Subalgebra.mem_comap]
    convert (A.restrictScalars â„).mul_mem hFA (star_mem hFA : star F âˆˆ A)
    ext1
    simp [â† RCLike.mul_conj]
  Â· -- And it also separates the points `xâ‚`, `xâ‚‚`
    simpa [F] using sub_ne_zero.mpr hf

variable [CompactSpace X]

/-- The Stone-Weierstrass approximation theorem, `RCLike` version, that a star subalgebra `A` of
`C(X, ğ•œ)`, where `X` is a compact topological space and `RCLike ğ•œ`, is dense if it separates
points. -/
theorem ContinuousMap.starSubalgebra_topologicalClosure_eq_top_of_separatesPoints
    (A : StarSubalgebra ğ•œ C(X, ğ•œ)) (hA : A.SeparatesPoints) : A.topologicalClosure = âŠ¤ := by
  rw [StarSubalgebra.eq_top_iff]
  -- Let `I` be the natural inclusion of `C(X, â„)` into `C(X, ğ•œ)`
  let I : C(X, â„) â†’â‚—[â„] C(X, ğ•œ) := ofRealCLM.compLeftContinuous â„ X
  -- The main point of the proof is that its range (i.e., every real-valued function) is contained
  -- in the closure of `A`
  have key : LinearMap.range I â‰¤ (A.toSubmodule.restrictScalars â„).topologicalClosure := by
    -- Let `Aâ‚€` be the subalgebra of `C(X, â„)` consisting of `A`'s purely real elements; it is the
    -- preimage of `A` under `I`.  In this argument we only need its submodule structure.
    let Aâ‚€ : Submodule â„ C(X, â„) := (A.toSubmodule.restrictScalars â„).comap I
    -- By `Subalgebra.SeparatesPoints.rclike_to_real`, this subalgebra also separates points, so
    -- we may apply the real Stone-Weierstrass result to it.
    have SW : Aâ‚€.topologicalClosure = âŠ¤ :=
      haveI := subalgebra_topologicalClosure_eq_top_of_separatesPoints _ hA.rclike_to_real
      congr_arg Subalgebra.toSubmodule this
    rw [â† Submodule.map_top, â† SW]
    -- So it suffices to prove that the image under `I` of the closure of `Aâ‚€` is contained in the
    -- closure of `A`, which follows by abstract nonsense
    have hâ‚ := Aâ‚€.topologicalClosure_map ((@ofRealCLM ğ•œ _).compLeftContinuousCompact X)
    have hâ‚‚ := (A.toSubmodule.restrictScalars â„).map_comap_le I
    exact hâ‚.trans (Submodule.topologicalClosure_mono hâ‚‚)
  -- In particular, for a function `f` in `C(X, ğ•œ)`, the real and imaginary parts of `f` are in the
  -- closure of `A`
  intro f
  let f_re : C(X, â„) := (âŸ¨RCLike.re, RCLike.reCLM.continuousâŸ© : C(ğ•œ, â„)).comp f
  let f_im : C(X, â„) := (âŸ¨RCLike.im, RCLike.imCLM.continuousâŸ© : C(ğ•œ, â„)).comp f
  have h_f_re : I f_re âˆˆ A.topologicalClosure := key âŸ¨f_re, rflâŸ©
  have h_f_im : I f_im âˆˆ A.topologicalClosure := key âŸ¨f_im, rflâŸ©
  -- So `f_re + I â€¢ f_im` is in the closure of `A`
  have := A.topologicalClosure.add_mem h_f_re (A.topologicalClosure.smul_mem h_f_im RCLike.I)
  rw [StarSubalgebra.mem_toSubalgebra] at this
  convert this
  -- And this, of course, is just `f`
  ext
  apply Eq.symm
  simp [I, f_re, f_im, mul_comm RCLike.I _]

end RCLike

section PolynomialFunctions

open StarSubalgebra Polynomial
open scoped Polynomial

/-- Polynomial functions in are dense in `C(s, â„)` when `s` is compact.

See `polynomialFunctions_closure_eq_top` for the special case `s = Set.Icc a b` which does not use
the full Stone-Weierstrass theorem. Of course, that version could be used to prove this one as
well. -/
theorem polynomialFunctions.topologicalClosure (s : Set â„)
    [CompactSpace s] : (polynomialFunctions s).topologicalClosure = âŠ¤ :=
  ContinuousMap.subalgebra_topologicalClosure_eq_top_of_separatesPoints _
    (polynomialFunctions_separatesPoints s)

/-- The star subalgebra generated by polynomials functions is dense in `C(s, ğ•œ)` when `s` is
compact and `ğ•œ` is either `â„` or `â„‚`. -/
theorem polynomialFunctions.starClosure_topologicalClosure {ğ•œ : Type*} [RCLike ğ•œ] (s : Set ğ•œ)
    [CompactSpace s] : (polynomialFunctions s).starClosure.topologicalClosure = âŠ¤ :=
  ContinuousMap.starSubalgebra_topologicalClosure_eq_top_of_separatesPoints _
    (Subalgebra.separatesPoints_monotone le_sup_left (polynomialFunctions_separatesPoints s))

/-- An induction principle for `C(s, ğ•œ)`. -/
@[elab_as_elim]
theorem ContinuousMap.induction_on {ğ•œ : Type*} [RCLike ğ•œ] {s : Set ğ•œ}
    {p : C(s, ğ•œ) â†’ Prop} (const : âˆ€ r, p (.const s r)) (id : p (.restrict s <| .id ğ•œ))
    (star_id : p (star (.restrict s <| .id ğ•œ)))
    (add : âˆ€ f g, p f â†’ p g â†’ p (f + g)) (mul : âˆ€ f g, p f â†’ p g â†’ p (f * g))
    (closure : (âˆ€ f âˆˆ (polynomialFunctions s).starClosure, p f) â†’ âˆ€ f, p f) (f : C(s, ğ•œ)) :
    p f := by
  refine closure (fun f hf => ?_) f
  rw [polynomialFunctions.starClosure_eq_adjoin_X] at hf
  induction hf using Algebra.adjoin_induction with
  | mem f hf =>
    simp only [Set.mem_union, Set.mem_singleton_iff, Set.mem_star] at hf
    rw [star_eq_iff_star_eq, eq_comm (b := f)] at hf
    obtain (rfl | rfl) := hf
    all_goals simpa only [toContinuousMapOnAlgHom_apply, toContinuousMapOn_X_eq_restrict_id]
  | algebraMap r => exact const r
  | add _ _ _ _ hf hg => exact add _ _ hf hg
  | mul _ _ _ _ hf hg => exact mul _ _ hf hg

open Topology in
@[elab_as_elim]
theorem ContinuousMap.induction_on_of_compact {ğ•œ : Type*} [RCLike ğ•œ] {s : Set ğ•œ} [CompactSpace s]
    {p : C(s, ğ•œ) â†’ Prop} (const : âˆ€ r, p (.const s r)) (id : p (.restrict s <| .id ğ•œ))
    (star_id : p (star (.restrict s <| .id ğ•œ)))
    (add : âˆ€ f g, p f â†’ p g â†’ p (f + g)) (mul : âˆ€ f g, p f â†’ p g â†’ p (f * g))
    (frequently : âˆ€ f, (âˆƒá¶  g in ğ“ f, p g) â†’ p f) (f : C(s, ğ•œ)) :
    p f := by
  refine f.induction_on const id star_id add mul fun h f â†¦ frequently f ?_
  have := polynomialFunctions.starClosure_topologicalClosure s â–¸ mem_top (x := f)
  rw [â† SetLike.mem_coe, topologicalClosure_coe, mem_closure_iff_frequently] at this
  exact this.mp <| .of_forall h

/-- Continuous algebra homomorphisms from `C(s, â„)` into an `â„`-algebra `A` which agree
at `X : ğ•œ[X]` (interpreted as a continuous map) are, in fact, equal. -/
@[ext (iff := false)]
theorem ContinuousMap.algHom_ext_map_X {A : Type*} [Ring A]
    [Algebra â„ A] [TopologicalSpace A] [T2Space A] {s : Set â„} [CompactSpace s]
    {Ï† Ïˆ : C(s, â„) â†’â‚[â„] A} (hÏ† : Continuous Ï†) (hÏˆ : Continuous Ïˆ)
    (h : Ï† (toContinuousMapOnAlgHom s X) = Ïˆ (toContinuousMapOnAlgHom s X)) : Ï† = Ïˆ := by
  suffices (âŠ¤ : Subalgebra â„ C(s, â„)) â‰¤ AlgHom.equalizer Ï† Ïˆ from
    AlgHom.ext fun x => this (by trivial)
  rw [â† polynomialFunctions.topologicalClosure s]
  exact Subalgebra.topologicalClosure_minimal (polynomialFunctions s)
    (polynomialFunctions.le_equalizer s Ï† Ïˆ h) (isClosed_eq hÏ† hÏˆ)

/-- Continuous star algebra homomorphisms from `C(s, ğ•œ)` into a star `ğ•œ`-algebra `A` which agree
at `X : ğ•œ[X]` (interpreted as a continuous map) are, in fact, equal. -/
@[ext (iff := false)]
theorem ContinuousMap.starAlgHom_ext_map_X {ğ•œ A : Type*} [RCLike ğ•œ] [Ring A] [StarRing A]
    [Algebra ğ•œ A] [TopologicalSpace A] [T2Space A] {s : Set ğ•œ} [CompactSpace s]
    {Ï† Ïˆ : C(s, ğ•œ) â†’â‹†â‚[ğ•œ] A} (hÏ† : Continuous Ï†) (hÏˆ : Continuous Ïˆ)
    (h : Ï† (toContinuousMapOnAlgHom s X) = Ïˆ (toContinuousMapOnAlgHom s X)) : Ï† = Ïˆ := by
  suffices (âŠ¤ : StarSubalgebra ğ•œ C(s, ğ•œ)) â‰¤ StarAlgHom.equalizer Ï† Ïˆ from
    StarAlgHom.ext fun x => this mem_top
  rw [â† polynomialFunctions.starClosure_topologicalClosure s]
  exact StarSubalgebra.topologicalClosure_minimal
    (polynomialFunctions.starClosure_le_equalizer s Ï† Ïˆ h) (isClosed_eq hÏ† hÏˆ)

end PolynomialFunctions

/-! ### Continuous maps sending zero to zero -/

section ContinuousMapZero

variable {ğ•œ : Type*} [RCLike ğ•œ]
open NonUnitalStarAlgebra Submodule

namespace ContinuousMap

lemma adjoin_id_eq_span_one_union (s : Set ğ•œ) :
    ((StarAlgebra.adjoin ğ•œ {(restrict s (.id ğ•œ) : C(s, ğ•œ))}) : Set C(s, ğ•œ)) =
      span ğ•œ ({(1 : C(s, ğ•œ))} âˆª (adjoin ğ•œ {(restrict s (.id ğ•œ) : C(s, ğ•œ))})) := by
  ext x
  rw [SetLike.mem_coe, SetLike.mem_coe, â† StarAlgebra.adjoin_nonUnitalStarSubalgebra,
    â† StarSubalgebra.mem_toSubalgebra, â† Subalgebra.mem_toSubmodule,
    StarAlgebra.adjoin_nonUnitalStarSubalgebra_eq_span, span_union, span_eq_toSubmodule]

open Pointwise in
lemma adjoin_id_eq_span_one_add (s : Set ğ•œ) :
    ((StarAlgebra.adjoin ğ•œ {(restrict s (.id ğ•œ) : C(s, ğ•œ))}) : Set C(s, ğ•œ)) =
      (span ğ•œ {(1 : C(s, ğ•œ))} : Set C(s, ğ•œ)) + (adjoin ğ•œ {(restrict s (.id ğ•œ) : C(s, ğ•œ))}) := by
  ext x
  rw [SetLike.mem_coe, â† StarAlgebra.adjoin_nonUnitalStarSubalgebra,
    â† StarSubalgebra.mem_toSubalgebra, â† Subalgebra.mem_toSubmodule,
    StarAlgebra.adjoin_nonUnitalStarSubalgebra_eq_span, mem_sup]
  simp [Set.mem_add]

lemma nonUnitalStarAlgebraAdjoin_id_subset_ker_evalStarAlgHom {s : Set ğ•œ} (h0 : 0 âˆˆ s) :
    (adjoin ğ•œ {restrict s (.id ğ•œ)} : Set C(s, ğ•œ)) âŠ†
      RingHom.ker (evalStarAlgHom ğ•œ ğ•œ (âŸ¨0, h0âŸ© : s)) := by
  intro f hf
  induction hf using adjoin_induction with
  | mem f hf =>
    obtain rfl := Set.mem_singleton_iff.mp hf
    rfl
  | add f g _ _ hf hg => exact add_mem hf hg
  | zero => exact zero_mem _
  | mul f g _ _ _ hg => exact Ideal.mul_mem_left _ f hg
  | smul r f _ hf =>
    rw [SetLike.mem_coe, RingHom.mem_ker] at hf âŠ¢
    rw [map_smul, hf, smul_zero]
  | star f _ hf =>
    rw [SetLike.mem_coe, RingHom.mem_ker] at hf âŠ¢
    rw [map_star, hf, star_zero]

lemma ker_evalStarAlgHom_inter_adjoin_id (s : Set ğ•œ) (h0 : 0 âˆˆ s) :
    (StarAlgebra.adjoin ğ•œ {restrict s (.id ğ•œ)} : Set C(s, ğ•œ)) âˆ©
      RingHom.ker (evalStarAlgHom ğ•œ ğ•œ (âŸ¨0, h0âŸ© : s)) = adjoin ğ•œ {restrict s (.id ğ•œ)} := by
  ext f
  constructor
  Â· rintro âŸ¨hfâ‚, hfâ‚‚âŸ©
    rw [SetLike.mem_coe] at hfâ‚‚ âŠ¢
    simp_rw [adjoin_id_eq_span_one_add, Set.mem_add, SetLike.mem_coe, mem_span_singleton] at hfâ‚
    obtain âŸ¨-, âŸ¨r, rflâŸ©, f, hf, rflâŸ© := hfâ‚
    have := nonUnitalStarAlgebraAdjoin_id_subset_ker_evalStarAlgHom h0 hf
    simp only [SetLike.mem_coe, RingHom.mem_ker, evalStarAlgHom_apply] at hfâ‚‚ this
    rw [add_apply, this, add_zero, smul_apply, one_apply, smul_eq_mul, mul_one] at hfâ‚‚
    rwa [hfâ‚‚, zero_smul, zero_add]
  Â· simp only [Set.mem_inter_iff, SetLike.mem_coe]
    refine fun hf â†¦ âŸ¨?_, nonUnitalStarAlgebraAdjoin_id_subset_ker_evalStarAlgHom h0 hfâŸ©
    exact adjoin_le_starAlgebra_adjoin _ _ hf

-- the statement should be in terms of non unital subalgebras, but we lack API
open RingHom Filter Topology in
theorem AlgHom.closure_ker_inter {F S K A : Type*} [CommRing K] [Ring A] [Algebra K A]
    [TopologicalSpace K] [T1Space K] [TopologicalSpace A] [ContinuousSub A] [ContinuousSMul K A]
    [FunLike F A K] [AlgHomClass F K A K] [SetLike S A] [OneMemClass S A] [AddSubgroupClass S A]
    [SMulMemClass S K A] (Ï† : F) (hÏ† : Continuous Ï†) (s : S) :
    closure (s âˆ© RingHom.ker Ï†) = closure s âˆ© (ker Ï† : Set A) := by
  refine subset_antisymm ?_ ?_
  Â· simpa only [ker_eq, (isClosed_singleton.preimage hÏ†).closure_eq]
      using closure_inter_subset_inter_closure s (ker Ï† : Set A)
  Â· intro x âŸ¨hxs, (hxÏ† : Ï† x = 0)âŸ©
    rw [mem_closure_iff_clusterPt, ClusterPt] at hxs
    have : Tendsto (fun y â†¦ y - Ï† y â€¢ 1) (ğ“ x âŠ“ ğ“Ÿ s) (ğ“ x) := by
      conv => congr; rfl; rfl; rw [â† sub_zero x, â† zero_smul K 1, â† hxÏ†]
      exact Filter.tendsto_inf_left (Continuous.tendsto (by fun_prop) x)
    refine mem_closure_of_tendsto this <| eventually_inf_principal.mpr ?_
    filter_upwards [] with g hg using
      âŸ¨sub_mem hg (SMulMemClass.smul_mem _ <| one_mem _), by simp [RingHom.mem_ker]âŸ©

lemma ker_evalStarAlgHom_eq_closure_adjoin_id (s : Set ğ•œ) (h0 : 0 âˆˆ s) [CompactSpace s] :
    (RingHom.ker (evalStarAlgHom ğ•œ ğ•œ (âŸ¨0, h0âŸ© : s)) : Set C(s, ğ•œ)) =
      closure (adjoin ğ•œ {(restrict s (.id ğ•œ))}) := by
  rw [â† ker_evalStarAlgHom_inter_adjoin_id s h0,
    AlgHom.closure_ker_inter (Ï† := evalStarAlgHom ğ•œ ğ•œ (X := s) âŸ¨0, h0âŸ©) (continuous_eval_const _) _]
  convert (Set.univ_inter _).symm
  rw [â† Polynomial.toContinuousMapOn_X_eq_restrict_id, â† Polynomial.toContinuousMapOnAlgHom_apply,
    â† polynomialFunctions.starClosure_eq_adjoin_X s]
  congrm(($(polynomialFunctions.starClosure_topologicalClosure s) : Set C(s, ğ•œ)))

end ContinuousMap

open scoped ContinuousMapZero

/-- If `s : Set ğ•œ` with `RCLike ğ•œ` is compact and contains `0`, then the non-unital star subalgebra
generated by the identity function in `C(s, ğ•œ)â‚€` is dense. This can be seen as a version of the
Weierstrass approximation theorem. -/
lemma ContinuousMapZero.adjoin_id_dense {s : Set ğ•œ} [Zero s] (h0 : ((0 : s) : ğ•œ) = 0)
    [CompactSpace s] : Dense (adjoin ğ•œ {(.id h0 : C(s, ğ•œ)â‚€)} : Set C(s, ğ•œ)â‚€) := by
  have h0' : 0 âˆˆ s := h0 â–¸ (0 : s).property
  rw [dense_iff_closure_eq,
    â† isClosedEmbedding_toContinuousMap.injective.preimage_image (closure _),
    â† isClosedEmbedding_toContinuousMap.closure_image_eq, â† coe_toContinuousMapHom,
    â† NonUnitalStarSubalgebra.coe_map, NonUnitalStarAlgHom.map_adjoin_singleton,
    toContinuousMapHom_apply, toContinuousMap_id h0,
    â† ContinuousMap.ker_evalStarAlgHom_eq_closure_adjoin_id s h0']
  apply Set.eq_univ_of_forall fun f â†¦ ?_
  simp only [Set.mem_preimage, toContinuousMapHom_apply, SetLike.mem_coe, RingHom.mem_ker,
    ContinuousMap.evalStarAlgHom_apply, ContinuousMap.coe_coe]
  rw [show âŸ¨0, h0'âŸ© = (0 : s) by ext; exact h0.symm, _root_.map_zero f]

/-- An induction principle for `C(s, ğ•œ)â‚€`. -/
@[elab_as_elim]
lemma ContinuousMapZero.induction_on {s : Set ğ•œ} [Zero s] (h0 : ((0 : s) : ğ•œ) = 0)
    {p : C(s, ğ•œ)â‚€ â†’ Prop} (zero : p 0) (id : p (.id h0)) (star_id : p (star (.id h0)))
    (add : âˆ€ f g, p f â†’ p g â†’ p (f + g)) (mul : âˆ€ f g, p f â†’ p g â†’ p (f * g))
    (smul : âˆ€ (r : ğ•œ) f, p f â†’ p (r â€¢ f))
    (closure : (âˆ€ f âˆˆ adjoin ğ•œ {(.id h0 : C(s, ğ•œ)â‚€)}, p f) â†’ âˆ€ f, p f) (f : C(s, ğ•œ)â‚€) :
    p f := by
  refine closure (fun f hf => ?_) f
  induction hf using NonUnitalAlgebra.adjoin_induction with
  | mem f hf =>
    simp only [Set.mem_union, Set.mem_singleton_iff, Set.mem_star] at hf
    rw [star_eq_iff_star_eq, eq_comm (b := f)] at hf
    obtain (rfl | rfl) := hf
    all_goals assumption
  | zero => exact zero
  | add _ _ _ _ hf hg => exact add _ _ hf hg
  | mul _ _ _ _ hf hg => exact mul _ _ hf hg
  | smul _ _ _ hf => exact smul _ _ hf

open Topology in
@[elab_as_elim]
theorem ContinuousMapZero.induction_on_of_compact {s : Set ğ•œ} [Zero s] (h0 : ((0 : s) : ğ•œ) = 0)
    [CompactSpace s] {p : C(s, ğ•œ)â‚€ â†’ Prop} (zero : p 0) (id : p (.id h0))
    (star_id : p (star (.id h0))) (add : âˆ€ f g, p f â†’ p g â†’ p (f + g))
    (mul : âˆ€ f g, p f â†’ p g â†’ p (f * g)) (smul : âˆ€ (r : ğ•œ) f, p f â†’ p (r â€¢ f))
    (frequently : âˆ€ f, (âˆƒá¶  g in ğ“ f, p g) â†’ p f) (f : C(s, ğ•œ)â‚€) :
    p f := by
  refine f.induction_on h0 zero id star_id add mul smul fun h f â†¦ frequently f ?_
  have := (ContinuousMapZero.adjoin_id_dense h0).closure_eq â–¸ Set.mem_univ (x := f)
  exact mem_closure_iff_frequently.mp this |>.mp <| .of_forall h

lemma ContinuousMapZero.nonUnitalStarAlgHom_apply_mul_eq_zero {ğ•œ A : Type*}
    [RCLike ğ•œ] [NonUnitalRing A] [StarRing A] [TopologicalSpace A] [TopologicalSemiring A]
    [T2Space A] [Module ğ•œ A] [IsScalarTower ğ•œ A A] {s : Set ğ•œ} [Zero s] [CompactSpace s]
    (h0 : (0 : s) = (0 : ğ•œ)) (Ï† : C(s, ğ•œ)â‚€ â†’â‹†â‚™â‚[ğ•œ] A) (a : A) (hmul_id : Ï† (.id h0) * a = 0)
    (hmul_star_id : Ï† (star (.id h0)) * a = 0) (hÏ† : Continuous Ï†) (f : C(s, ğ•œ)â‚€) :
    Ï† f * a = 0 := by
  induction f using ContinuousMapZero.induction_on_of_compact h0 with
  | zero => simp [map_zero]
  | id => exact hmul_id
  | star_id => exact hmul_star_id
  | add _ _ hâ‚ hâ‚‚ => simp only [map_add, add_mul, hâ‚, hâ‚‚, zero_add]
  | mul _ _ _ h => simp only [map_mul, mul_assoc, h, mul_zero]
  | smul _ _ h => rw [map_smul, smul_mul_assoc, h, smul_zero]
  | frequently f h => exact h.mem_of_closed <| isClosed_eq (by fun_prop) continuous_zero

lemma ContinuousMapZero.mul_nonUnitalStarAlgHom_apply_eq_zero {ğ•œ A : Type*}
    [RCLike ğ•œ] [NonUnitalRing A] [StarRing A] [TopologicalSpace A] [TopologicalSemiring A]
    [T2Space A] [Module ğ•œ A] [SMulCommClass ğ•œ A A] {s : Set ğ•œ} [Zero s] [CompactSpace s]
    (h0 : (0 : s) = (0 : ğ•œ)) (Ï† : C(s, ğ•œ)â‚€ â†’â‹†â‚™â‚[ğ•œ] A) (a : A) (hmul_id : a * Ï† (.id h0) = 0)
    (hmul_star_id : a * Ï† (star (.id h0)) = 0) (hÏ† : Continuous Ï†) (f : C(s, ğ•œ)â‚€) :
    a * Ï† f = 0 := by
  induction f using ContinuousMapZero.induction_on_of_compact h0 with
  | zero => simp [map_zero]
  | id => exact hmul_id
  | star_id => exact hmul_star_id
  | add _ _ hâ‚ hâ‚‚ => simp only [map_add, mul_add, hâ‚, hâ‚‚, zero_add]
  | mul _ _ h _ => simp only [map_mul, â† mul_assoc, h, zero_mul]
  | smul _ _ h => rw [map_smul, mul_smul_comm, h, smul_zero]
  | frequently f h => exact h.mem_of_closed <| isClosed_eq (by fun_prop) continuous_zero

end ContinuousMapZero
