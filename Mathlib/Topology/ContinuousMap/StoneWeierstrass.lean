/-
Copyright (c) 2021 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Heather Macbeth
-/
import Mathlib.Algebra.Algebra.Subalgebra.Tower
import Mathlib.Analysis.RCLike.Basic
import Mathlib.Topology.Algebra.Star.Real
import Mathlib.Topology.Algebra.StarSubalgebra
import Mathlib.Topology.ContinuousMap.ContinuousMapZero
import Mathlib.Topology.ContinuousMap.Lattice
import Mathlib.Topology.ContinuousMap.Weierstrass

/-!
# The Stone-Weierstrass theorem

If a subalgebra `A` of `C(X, ‚Ñù)`, where `X` is a compact topological space,
separates points, then it is dense.

We argue as follows.

* In any subalgebra `A` of `C(X, ‚Ñù)`, if `f ‚àà A`, then `abs f ‚àà A.topologicalClosure`.
  This follows from the Weierstrass approximation theorem on `[-‚Äñf‚Äñ, ‚Äñf‚Äñ]` by
  approximating `abs` uniformly thereon by polynomials.
* This ensures that `A.topologicalClosure` is actually a sublattice:
  if it contains `f` and `g`, then it contains the pointwise supremum `f ‚äî g`
  and the pointwise infimum `f ‚äì g`.
* Any nonempty sublattice `L` of `C(X, ‚Ñù)` which separates points is dense,
  by a nice argument approximating a given `f` above and below using separating functions.
  For each `x y : X`, we pick a function `g x y ‚àà L` so `g x y x = f x` and `g x y y = f y`.
  By continuity these functions remain close to `f` on small patches around `x` and `y`.
  We use compactness to identify a certain finitely indexed infimum of finitely indexed supremums
  which is then close to `f` everywhere, obtaining the desired approximation.
* Finally we put these pieces together. `L = A.topologicalClosure` is a nonempty sublattice
  which separates points since `A` does, and so is dense (in fact equal to `‚ä§`).

We then prove the complex version for star subalgebras `A`, by separately approximating
the real and imaginary parts using the real subalgebra of real-valued functions in `A`
(which still separates points, by taking the norm-square of a separating function).

## Future work

Extend to cover the case of subalgebras of the continuous functions vanishing at infinity,
on non-compact spaces.

-/

assert_not_exists Unitization

noncomputable section

namespace ContinuousMap

variable {X : Type*} [TopologicalSpace X] [CompactSpace X]

open scoped Polynomial

/-- Turn a function `f : C(X, ‚Ñù)` into a continuous map into `Set.Icc (-‚Äñf‚Äñ) (‚Äñf‚Äñ)`,
thereby explicitly attaching bounds.
-/
def attachBound (f : C(X, ‚Ñù)) : C(X, Set.Icc (-‚Äñf‚Äñ) ‚Äñf‚Äñ) where
  toFun x := ‚ü®f x, ‚ü®neg_norm_le_apply f x, apply_le_norm f x‚ü©‚ü©

@[simp]
theorem attachBound_apply_coe (f : C(X, ‚Ñù)) (x : X) : ((attachBound f) x : ‚Ñù) = f x :=
  rfl

theorem polynomial_comp_attachBound (A : Subalgebra ‚Ñù C(X, ‚Ñù)) (f : A) (g : ‚Ñù[X]) :
    (g.toContinuousMapOn (Set.Icc (-‚Äñf‚Äñ) ‚Äñf‚Äñ)).comp (f : C(X, ‚Ñù)).attachBound =
      Polynomial.aeval f g := by
  ext
  simp only [Polynomial.aeval_subalgebra_coe, Polynomial.aeval_continuousMap_apply]
  simp

/-- Given a continuous function `f` in a subalgebra of `C(X, ‚Ñù)`, postcomposing by a polynomial
gives another function in `A`.

This lemma proves something slightly more subtle than this:
we take `f`, and think of it as a function into the restricted target `Set.Icc (-‚Äñf‚Äñ) ‚Äñf‚Äñ)`,
and then postcompose with a polynomial function on that interval.
This is in fact the same situation as above, and so also gives a function in `A`.
-/
theorem polynomial_comp_attachBound_mem (A : Subalgebra ‚Ñù C(X, ‚Ñù)) (f : A) (g : ‚Ñù[X]) :
    (g.toContinuousMapOn (Set.Icc (-‚Äñf‚Äñ) ‚Äñf‚Äñ)).comp (f : C(X, ‚Ñù)).attachBound ‚àà A := by
  rw [polynomial_comp_attachBound]
  apply SetLike.coe_mem

theorem comp_attachBound_mem_closure (A : Subalgebra ‚Ñù C(X, ‚Ñù)) (f : A)
    (p : C(Set.Icc (-‚Äñf‚Äñ) ‚Äñf‚Äñ, ‚Ñù)) : p.comp (attachBound (f : C(X, ‚Ñù))) ‚àà A.topologicalClosure := by
  -- `p` itself is in the closure of polynomials, by the Weierstrass theorem,
  have mem_closure : p ‚àà (polynomialFunctions (Set.Icc (-‚Äñf‚Äñ) ‚Äñf‚Äñ)).topologicalClosure :=
    continuousMap_mem_polynomialFunctions_closure _ _ p
  -- and so there are polynomials arbitrarily close.
  have frequently_mem_polynomials := mem_closure_iff_frequently.mp mem_closure
  -- To prove `p.comp (attachBound f)` is in the closure of `A`,
  -- we show there are elements of `A` arbitrarily close.
  apply mem_closure_iff_frequently.mpr
  -- To show that, we pull back the polynomials close to `p`,
  refine
    ((compRightContinuousMap ‚Ñù (attachBound (f : C(X, ‚Ñù)))).continuousAt
            p).tendsto.frequently_map
      _ ?_ frequently_mem_polynomials
  -- but need to show that those pullbacks are actually in `A`.
  rintro _ ‚ü®g, ‚ü®-, rfl‚ü©‚ü©
  simp only [SetLike.mem_coe, AlgHom.coe_toRingHom, compRightContinuousMap_apply,
    Polynomial.toContinuousMapOnAlgHom_apply]
  apply polynomial_comp_attachBound_mem

theorem abs_mem_subalgebra_closure (A : Subalgebra ‚Ñù C(X, ‚Ñù)) (f : A) :
    |(f : C(X, ‚Ñù))| ‚àà A.topologicalClosure := by
  let f' := attachBound (f : C(X, ‚Ñù))
  let abs : C(Set.Icc (-‚Äñf‚Äñ) ‚Äñf‚Äñ, ‚Ñù) := { toFun := fun x : Set.Icc (-‚Äñf‚Äñ) ‚Äñf‚Äñ => |(x : ‚Ñù)| }
  change abs.comp f' ‚àà A.topologicalClosure
  apply comp_attachBound_mem_closure

theorem inf_mem_subalgebra_closure (A : Subalgebra ‚Ñù C(X, ‚Ñù)) (f g : A) :
    (f : C(X, ‚Ñù)) ‚äì (g : C(X, ‚Ñù)) ‚àà A.topologicalClosure := by
  rw [inf_eq_half_smul_add_sub_abs_sub' ‚Ñù]
  refine
    A.topologicalClosure.smul_mem
      (A.topologicalClosure.sub_mem
        (A.topologicalClosure.add_mem (A.le_topologicalClosure f.property)
          (A.le_topologicalClosure g.property))
        ?_)
      _
  exact mod_cast abs_mem_subalgebra_closure A _

theorem inf_mem_closed_subalgebra (A : Subalgebra ‚Ñù C(X, ‚Ñù)) (h : IsClosed (A : Set C(X, ‚Ñù)))
    (f g : A) : (f : C(X, ‚Ñù)) ‚äì (g : C(X, ‚Ñù)) ‚àà A := by
  convert inf_mem_subalgebra_closure A f g
  apply SetLike.ext'
  symm
  rw [Subalgebra.topologicalClosure_coe, closure_eq_iff_isClosed]
  exact h

theorem sup_mem_subalgebra_closure (A : Subalgebra ‚Ñù C(X, ‚Ñù)) (f g : A) :
    (f : C(X, ‚Ñù)) ‚äî (g : C(X, ‚Ñù)) ‚àà A.topologicalClosure := by
  rw [sup_eq_half_smul_add_add_abs_sub' ‚Ñù]
  refine
    A.topologicalClosure.smul_mem
      (A.topologicalClosure.add_mem
        (A.topologicalClosure.add_mem (A.le_topologicalClosure f.property)
          (A.le_topologicalClosure g.property))
        ?_)
      _
  exact mod_cast abs_mem_subalgebra_closure A _

theorem sup_mem_closed_subalgebra (A : Subalgebra ‚Ñù C(X, ‚Ñù)) (h : IsClosed (A : Set C(X, ‚Ñù)))
    (f g : A) : (f : C(X, ‚Ñù)) ‚äî (g : C(X, ‚Ñù)) ‚àà A := by
  convert sup_mem_subalgebra_closure A f g
  apply SetLike.ext'
  symm
  dsimp
  rw [closure_eq_iff_isClosed]
  exact h

open scoped Topology

-- Here's the fun part of Stone-Weierstrass!
theorem sublattice_closure_eq_top (L : Set C(X, ‚Ñù)) (nA : L.Nonempty)
    (inf_mem : ‚àÄ·µâ (f ‚àà L) (g ‚àà L), f ‚äì g ‚àà L)
    (sup_mem : ‚àÄ·µâ (f ‚àà L) (g ‚àà L), f ‚äî g ‚àà L) (sep : L.SeparatesPointsStrongly) :
    closure L = ‚ä§ := by
  -- We start by boiling down to a statement about close approximation.
  rw [eq_top_iff]
  rintro f -
  refine
    Filter.Frequently.mem_closure
      ((Filter.HasBasis.frequently_iff Metric.nhds_basis_ball).mpr fun Œµ pos => ?_)
  simp only [Metric.mem_ball]
  -- It will be helpful to assume `X` is nonempty later,
  -- so we get that out of the way here.
  by_cases nX : Nonempty X
  swap
  ¬∑ exact ‚ü®nA.some, (dist_lt_iff pos).mpr fun x => False.elim (nX ‚ü®x‚ü©), nA.choose_spec‚ü©
  /-
    The strategy now is to pick a family of continuous functions `g x y` in `A`
    with the property that `g x y x = f x` and `g x y y = f y`
    (this is immediate from `h : SeparatesPointsStrongly`)
    then use continuity to see that `g x y` is close to `f` near both `x` and `y`,
    and finally using compactness to produce the desired function `h`
    as a maximum over finitely many `x` of a minimum over finitely many `y` of the `g x y`.
    -/
  dsimp only [Set.SeparatesPointsStrongly] at sep
  choose g hg w‚ÇÅ w‚ÇÇ using sep f
  -- For each `x y`, we define `U x y` to be `{z | f z - Œµ < g x y z}`,
  -- and observe this is a neighbourhood of `y`.
  let U : X ‚Üí X ‚Üí Set X := fun x y => {z | f z - Œµ < g x y z}
  have U_nhds_y : ‚àÄ x y, U x y ‚àà ùìù y := by
    intro x y
    refine IsOpen.mem_nhds ?_ ?_
    ¬∑ apply isOpen_lt <;> fun_prop
    ¬∑ rw [Set.mem_setOf_eq, w‚ÇÇ]
      exact sub_lt_self _ pos
  -- Fixing `x` for a moment, we have a family of functions `fun y ‚Ü¶ g x y`
  -- which on different patches (the `U x y`) are greater than `f z - Œµ`.
  -- Taking the supremum of these functions
  -- indexed by a finite collection of patches which cover `X`
  -- will give us an element of `A` that is globally greater than `f z - Œµ`
  -- and still equal to `f x` at `x`.
  -- Since `X` is compact, for every `x` there is some finset `ys t`
  -- so the union of the `U x y` for `y ‚àà ys x` still covers everything.
  let ys : X ‚Üí Finset X := fun x => (CompactSpace.elim_nhds_subcover (U x) (U_nhds_y x)).choose
  let ys_w : ‚àÄ x, ‚ãÉ y ‚àà ys x, U x y = ‚ä§ := fun x =>
    (CompactSpace.elim_nhds_subcover (U x) (U_nhds_y x)).choose_spec
  have ys_nonempty : ‚àÄ x, (ys x).Nonempty := fun x =>
    Set.nonempty_of_union_eq_top_of_nonempty _ _ nX (ys_w x)
  -- Thus for each `x` we have the desired `h x : A` so `f z - Œµ < h x z` everywhere
  -- and `h x x = f x`.
  let h : X ‚Üí L := fun x =>
    ‚ü®(ys x).sup' (ys_nonempty x) fun y => (g x y : C(X, ‚Ñù)),
      Finset.sup'_mem _ sup_mem _ _ _ fun y _ => hg x y‚ü©
  have lt_h : ‚àÄ x z, f z - Œµ < (h x : X ‚Üí ‚Ñù) z := by
    intro x z
    obtain ‚ü®y, ym, zm‚ü© := Set.exists_set_mem_of_union_eq_top _ _ (ys_w x) z
    dsimp [h]
    simp only [coe_sup', Finset.sup'_apply, Finset.lt_sup'_iff]
    exact ‚ü®y, ym, zm‚ü©
  have h_eq : ‚àÄ x, (h x : X ‚Üí ‚Ñù) x = f x := by intro x; simp [h, w‚ÇÅ]
  -- For each `x`, we define `W x` to be `{z | h x z < f z + Œµ}`,
  let W : X ‚Üí Set X := fun x => {z | (h x : X ‚Üí ‚Ñù) z < f z + Œµ}
  -- This is still a neighbourhood of `x`.
  have W_nhds : ‚àÄ x, W x ‚àà ùìù x := by
    intro x
    refine IsOpen.mem_nhds ?_ ?_
    ¬∑ apply isOpen_lt <;> fun_prop
    ¬∑ dsimp only [W, Set.mem_setOf_eq]
      rw [h_eq]
      exact lt_add_of_pos_right _ pos
  -- Since `X` is compact, there is some finset `ys t`
  -- so the union of the `W x` for `x ‚àà xs` still covers everything.
  let xs : Finset X := (CompactSpace.elim_nhds_subcover W W_nhds).choose
  let xs_w : ‚ãÉ x ‚àà xs, W x = ‚ä§ := (CompactSpace.elim_nhds_subcover W W_nhds).choose_spec
  have xs_nonempty : xs.Nonempty := Set.nonempty_of_union_eq_top_of_nonempty _ _ nX xs_w
  -- Finally our candidate function is the infimum over `x ‚àà xs` of the `h x`.
  -- This function is then globally less than `f z + Œµ`.
  let k : (L : Type _) :=
    ‚ü®xs.inf' xs_nonempty fun x => (h x : C(X, ‚Ñù)),
      Finset.inf'_mem _ inf_mem _ _ _ fun x _ => (h x).2‚ü©
  refine ‚ü®k.1, ?_, k.2‚ü©
  -- We just need to verify the bound, which we do pointwise.
  rw [dist_lt_iff pos]
  intro z
  -- We rewrite into this particular form,
  -- so that simp lemmas about inequalities involving `Finset.inf'` can fire.
  rw [show ‚àÄ a b Œµ : ‚Ñù, dist a b < Œµ ‚Üî a < b + Œµ ‚àß b - Œµ < a by
        intros; simp only [‚Üê Metric.mem_ball, Real.ball_eq_Ioo, Set.mem_Ioo, and_comm]]
  fconstructor
  ¬∑ dsimp
    simp only [k, Finset.inf'_lt_iff, ContinuousMap.inf'_apply]
    exact Set.exists_set_mem_of_union_eq_top _ _ xs_w z
  ¬∑ dsimp
    simp only [k, Finset.lt_inf'_iff, ContinuousMap.inf'_apply]
    rintro x -
    apply lt_h

/-- The **Stone-Weierstrass Approximation Theorem**,
that a subalgebra `A` of `C(X, ‚Ñù)`, where `X` is a compact topological space,
is dense if it separates points.
-/
theorem subalgebra_topologicalClosure_eq_top_of_separatesPoints (A : Subalgebra ‚Ñù C(X, ‚Ñù))
    (w : A.SeparatesPoints) : A.topologicalClosure = ‚ä§ := by
  -- The closure of `A` is closed under taking `sup` and `inf`,
  -- and separates points strongly (since `A` does),
  -- so we can apply `sublattice_closure_eq_top`.
  apply SetLike.ext'
  let L := A.topologicalClosure
  have n : Set.Nonempty (L : Set C(X, ‚Ñù)) := ‚ü®(1 : C(X, ‚Ñù)), A.le_topologicalClosure A.one_mem‚ü©
  convert
    sublattice_closure_eq_top (L : Set C(X, ‚Ñù)) n
      (fun f fm g gm => inf_mem_closed_subalgebra L A.isClosed_topologicalClosure ‚ü®f, fm‚ü© ‚ü®g, gm‚ü©)
      (fun f fm g gm => sup_mem_closed_subalgebra L A.isClosed_topologicalClosure ‚ü®f, fm‚ü© ‚ü®g, gm‚ü©)
      (Subalgebra.SeparatesPoints.strongly
        (Subalgebra.separatesPoints_monotone A.le_topologicalClosure w))
  simp [L]

/-- An alternative statement of the Stone-Weierstrass theorem.

If `A` is a subalgebra of `C(X, ‚Ñù)` which separates points (and `X` is compact),
every real-valued continuous function on `X` is a uniform limit of elements of `A`.
-/
theorem continuousMap_mem_subalgebra_closure_of_separatesPoints (A : Subalgebra ‚Ñù C(X, ‚Ñù))
    (w : A.SeparatesPoints) (f : C(X, ‚Ñù)) : f ‚àà A.topologicalClosure := by
  rw [subalgebra_topologicalClosure_eq_top_of_separatesPoints A w]
  simp

/-- An alternative statement of the Stone-Weierstrass theorem,
for those who like their epsilons.

If `A` is a subalgebra of `C(X, ‚Ñù)` which separates points (and `X` is compact),
every real-valued continuous function on `X` is within any `Œµ > 0` of some element of `A`.
-/
theorem exists_mem_subalgebra_near_continuousMap_of_separatesPoints (A : Subalgebra ‚Ñù C(X, ‚Ñù))
    (w : A.SeparatesPoints) (f : C(X, ‚Ñù)) (Œµ : ‚Ñù) (pos : 0 < Œµ) :
    ‚àÉ g : A, ‚Äñ(g : C(X, ‚Ñù)) - f‚Äñ < Œµ := by
  have w :=
    mem_closure_iff_frequently.mp (continuousMap_mem_subalgebra_closure_of_separatesPoints A w f)
  rw [Metric.nhds_basis_ball.frequently_iff] at w
  obtain ‚ü®g, H, m‚ü© := w Œµ pos
  rw [Metric.mem_ball, dist_eq_norm] at H
  exact ‚ü®‚ü®g, m‚ü©, H‚ü©

/-- An alternative statement of the Stone-Weierstrass theorem,
for those who like their epsilons and don't like bundled continuous functions.

If `A` is a subalgebra of `C(X, ‚Ñù)` which separates points (and `X` is compact),
every real-valued continuous function on `X` is within any `Œµ > 0` of some element of `A`.
-/
theorem exists_mem_subalgebra_near_continuous_of_separatesPoints (A : Subalgebra ‚Ñù C(X, ‚Ñù))
    (w : A.SeparatesPoints) (f : X ‚Üí ‚Ñù) (c : Continuous f) (Œµ : ‚Ñù) (pos : 0 < Œµ) :
    ‚àÉ g : A, ‚àÄ x, ‚Äñ(g : X ‚Üí ‚Ñù) x - f x‚Äñ < Œµ := by
  obtain ‚ü®g, b‚ü© := exists_mem_subalgebra_near_continuousMap_of_separatesPoints A w ‚ü®f, c‚ü© Œµ pos
  use g
  rwa [norm_lt_iff _ pos] at b

/-- A variant of the Stone-Weierstrass theorem where `X` need not be compact:
If `A` is a subalgebra of `C(X, ‚Ñù)` which separates points, then, for any compact set `K ‚äÜ X`,
every real-valued continuous function on `X` is within any `Œµ > 0` of some element of `A` on `K`. -/
theorem exists_mem_subalgebra_near_continuous_of_isCompact_of_separatesPoints
    {X : Type*} [TopologicalSpace X] {A : Subalgebra ‚Ñù C(X, ‚Ñù)} (hA : A.SeparatesPoints)
    (f : C(X, ‚Ñù)) {K : Set X} (hK : IsCompact K) {Œµ : ‚Ñù} (pos : 0 < Œµ) :
    ‚àÉ g ‚àà A, ‚àÄ x ‚àà K, ‚Äñ(g : X ‚Üí ‚Ñù) x - f x‚Äñ < Œµ := by
  let restrict_on_K : C(X, ‚Ñù) ‚Üí‚ãÜ‚Çê[‚Ñù] C(K, ‚Ñù) :=
    ContinuousMap.compStarAlgHom' ‚Ñù ‚Ñù ‚ü®(Subtype.val), continuous_subtype_val‚ü©
  --consider the subalgebra AK of functions with domain K
  let AK : Subalgebra ‚Ñù C(K, ‚Ñù) := Subalgebra.map (restrict_on_K) A
  have hsep : AK.SeparatesPoints := by
    intro x y hxy
    obtain ‚ü®_, ‚ü®g, hg1, hg2‚ü©, hg_sep‚ü© := hA (Subtype.coe_ne_coe.mpr hxy)
    simp only [Set.mem_image, SetLike.mem_coe, exists_exists_and_eq_and]
    use restrict_on_K g
    refine ‚ü®Subalgebra.mem_map.mpr ?_,
      by simpa only [compStarAlgHom'_apply, comp_apply, coe_mk, ne_eq, restrict_on_K, hg2]‚ü©
    use g, hg1
    simp [AlgHom.coe_coe]
  obtain ‚ü®‚ü®gK, hgKAK‚ü©, hgapprox‚ü© :=
    @ContinuousMap.exists_mem_subalgebra_near_continuous_of_separatesPoints _ _
    (isCompact_iff_compactSpace.mp hK) AK hsep (K.restrict f)
    (ContinuousOn.restrict (Continuous.continuousOn f.continuous)) Œµ pos
  obtain ‚ü®g, hgA, hgKAK‚ü© := Subalgebra.mem_map.mp hgKAK
  use g, hgA
  intro x hxK
  have eqg : g x = gK ‚ü®x, hxK‚ü© := by
    rw [‚Üê hgKAK]; rfl
  rw [eqg]
  exact hgapprox ‚ü®x, hxK‚ü©

end ContinuousMap

section RCLike

open RCLike

-- Redefine `X`, since for the next lemma it need not be compact
variable {ùïú : Type*} {X : Type*} [RCLike ùïú] [TopologicalSpace X]

open ContinuousMap

/- a post-port refactor eliminated `conjInvariantSubalgebra`, which was only used to
state and prove the Stone-Weierstrass theorem, in favor of using `StarSubalgebra`s,
which didn't exist at the time Stone-Weierstrass was written. -/


/-- If a star subalgebra of `C(X, ùïú)` separates points, then the real subalgebra
of its purely real-valued elements also separates points. -/
theorem Subalgebra.SeparatesPoints.rclike_to_real {A : StarSubalgebra ùïú C(X, ùïú)}
    (hA : A.SeparatesPoints) :
      ((A.restrictScalars ‚Ñù).comap
        (ofRealAm.compLeftContinuous ‚Ñù continuous_ofReal)).SeparatesPoints := by
  intro x‚ÇÅ x‚ÇÇ hx
  -- Let `f` in the subalgebra `A` separate the points `x‚ÇÅ`, `x‚ÇÇ`
  obtain ‚ü®_, ‚ü®f, hfA, rfl‚ü©, hf‚ü© := hA hx
  let F : C(X, ùïú) := f - const _ (f x‚ÇÇ)
  -- Subtract the constant `f x‚ÇÇ` from `f`; this is still an element of the subalgebra
  have hFA : F ‚àà A := by
    refine A.sub_mem hfA (@Eq.subst _ (¬∑ ‚àà A) _ _ ?_ <| A.smul_mem A.one_mem <| f x‚ÇÇ)
    ext1
    simp only [smul_apply, one_apply, Algebra.id.smul_eq_mul, mul_one,
      const_apply]
  -- Consider now the function `fun x ‚Ü¶ |f x - f x‚ÇÇ| ^ 2`
  refine ‚ü®_, ‚ü®‚ü®(‚ÄñF ¬∑‚Äñ ^ 2), by fun_prop‚ü©, ?_, rfl‚ü©, ?_‚ü©
  ¬∑ -- This is also an element of the subalgebra, and takes only real values
    rw [SetLike.mem_coe, Subalgebra.mem_comap]
    convert (A.restrictScalars ‚Ñù).mul_mem hFA (star_mem hFA : star F ‚àà A)
    ext1
    simp [‚Üê RCLike.mul_conj]
  ¬∑ -- And it also separates the points `x‚ÇÅ`, `x‚ÇÇ`
    simpa [F] using sub_ne_zero.mpr hf

variable [CompactSpace X]

/-- The Stone-Weierstrass approximation theorem, `RCLike` version, that a star subalgebra `A` of
`C(X, ùïú)`, where `X` is a compact topological space and `RCLike ùïú`, is dense if it separates
points. -/
theorem ContinuousMap.starSubalgebra_topologicalClosure_eq_top_of_separatesPoints
    (A : StarSubalgebra ùïú C(X, ùïú)) (hA : A.SeparatesPoints) : A.topologicalClosure = ‚ä§ := by
  rw [StarSubalgebra.eq_top_iff]
  -- Let `I` be the natural inclusion of `C(X, ‚Ñù)` into `C(X, ùïú)`
  let I : C(X, ‚Ñù) ‚ÜíL[‚Ñù] C(X, ùïú) := ofRealCLM.compLeftContinuous ‚Ñù X
  -- The main point of the proof is that its range (i.e., every real-valued function) is contained
  -- in the closure of `A`
  have key : LinearMap.range I ‚â§ (A.toSubmodule.restrictScalars ‚Ñù).topologicalClosure := by
    -- Let `A‚ÇÄ` be the subalgebra of `C(X, ‚Ñù)` consisting of `A`'s purely real elements; it is the
    -- preimage of `A` under `I`.  In this argument we only need its submodule structure.
    let A‚ÇÄ : Submodule ‚Ñù C(X, ‚Ñù) := (A.toSubmodule.restrictScalars ‚Ñù).comap I
    -- By `Subalgebra.SeparatesPoints.rclike_to_real`, this subalgebra also separates points, so
    -- we may apply the real Stone-Weierstrass result to it.
    have SW : A‚ÇÄ.topologicalClosure = ‚ä§ :=
      haveI := subalgebra_topologicalClosure_eq_top_of_separatesPoints _ hA.rclike_to_real
      congr_arg Subalgebra.toSubmodule this
    rw [‚Üê Submodule.map_top, ‚Üê SW]
    -- So it suffices to prove that the image under `I` of the closure of `A‚ÇÄ` is contained in the
    -- closure of `A`, which follows by abstract nonsense
    have h‚ÇÅ := A‚ÇÄ.topologicalClosure_map I
    have h‚ÇÇ := (A.toSubmodule.restrictScalars ‚Ñù).map_comap_le I
    exact h‚ÇÅ.trans (Submodule.topologicalClosure_mono h‚ÇÇ)
  -- In particular, for a function `f` in `C(X, ùïú)`, the real and imaginary parts of `f` are in the
  -- closure of `A`
  intro f
  let f_re : C(X, ‚Ñù) := (‚ü®RCLike.re, RCLike.reCLM.continuous‚ü© : C(ùïú, ‚Ñù)).comp f
  let f_im : C(X, ‚Ñù) := (‚ü®RCLike.im, RCLike.imCLM.continuous‚ü© : C(ùïú, ‚Ñù)).comp f
  have h_f_re : I f_re ‚àà A.topologicalClosure := key ‚ü®f_re, rfl‚ü©
  have h_f_im : I f_im ‚àà A.topologicalClosure := key ‚ü®f_im, rfl‚ü©
  -- So `f_re + I ‚Ä¢ f_im` is in the closure of `A`
  have := A.topologicalClosure.add_mem h_f_re (A.topologicalClosure.smul_mem h_f_im RCLike.I)
  rw [StarSubalgebra.mem_toSubalgebra] at this
  convert this
  -- And this, of course, is just `f`
  ext
  apply Eq.symm
  simp [I, f_re, f_im, mul_comm RCLike.I _]

end RCLike

section PolynomialFunctions

open StarSubalgebra Polynomial
open scoped Polynomial

/-- Polynomial functions in are dense in `C(s, ‚Ñù)` when `s` is compact.

See `polynomialFunctions_closure_eq_top` for the special case `s = Set.Icc a b` which does not use
the full Stone-Weierstrass theorem. Of course, that version could be used to prove this one as
well. -/
theorem polynomialFunctions.topologicalClosure (s : Set ‚Ñù)
    [CompactSpace s] : (polynomialFunctions s).topologicalClosure = ‚ä§ :=
  ContinuousMap.subalgebra_topologicalClosure_eq_top_of_separatesPoints _
    (polynomialFunctions_separatesPoints s)

/-- The star subalgebra generated by polynomials functions is dense in `C(s, ùïú)` when `s` is
compact and `ùïú` is either `‚Ñù` or `‚ÑÇ`. -/
theorem polynomialFunctions.starClosure_topologicalClosure {ùïú : Type*} [RCLike ùïú] (s : Set ùïú)
    [CompactSpace s] : (polynomialFunctions s).starClosure.topologicalClosure = ‚ä§ :=
  ContinuousMap.starSubalgebra_topologicalClosure_eq_top_of_separatesPoints _
    (Subalgebra.separatesPoints_monotone le_sup_left (polynomialFunctions_separatesPoints s))

/-- An induction principle for `C(s, ùïú)`. -/
@[elab_as_elim]
theorem ContinuousMap.induction_on {ùïú : Type*} [RCLike ùïú] {s : Set ùïú}
    {p : C(s, ùïú) ‚Üí Prop} (const : ‚àÄ r, p (.const s r)) (id : p (.restrict s <| .id ùïú))
    (star_id : p (star (.restrict s <| .id ùïú)))
    (add : ‚àÄ f g, p f ‚Üí p g ‚Üí p (f + g)) (mul : ‚àÄ f g, p f ‚Üí p g ‚Üí p (f * g))
    (closure : (‚àÄ f ‚àà (polynomialFunctions s).starClosure, p f) ‚Üí ‚àÄ f, p f) (f : C(s, ùïú)) :
    p f := by
  refine closure (fun f hf => ?_) f
  rw [polynomialFunctions.starClosure_eq_adjoin_X] at hf
  induction hf using Algebra.adjoin_induction with
  | mem f hf =>
    simp only [Set.mem_union, Set.mem_singleton_iff, Set.mem_star] at hf
    rw [star_eq_iff_star_eq, eq_comm (b := f)] at hf
    obtain (rfl | rfl) := hf
    all_goals simpa only [toContinuousMapOnAlgHom_apply, toContinuousMapOn_X_eq_restrict_id]
  | algebraMap r => exact const r
  | add _ _ _ _ hf hg => exact add _ _ hf hg
  | mul _ _ _ _ hf hg => exact mul _ _ hf hg

open Topology in
@[elab_as_elim]
theorem ContinuousMap.induction_on_of_compact {ùïú : Type*} [RCLike ùïú] {s : Set ùïú} [CompactSpace s]
    {p : C(s, ùïú) ‚Üí Prop} (const : ‚àÄ r, p (.const s r)) (id : p (.restrict s <| .id ùïú))
    (star_id : p (star (.restrict s <| .id ùïú)))
    (add : ‚àÄ f g, p f ‚Üí p g ‚Üí p (f + g)) (mul : ‚àÄ f g, p f ‚Üí p g ‚Üí p (f * g))
    (frequently : ‚àÄ f, (‚àÉ·∂† g in ùìù f, p g) ‚Üí p f) (f : C(s, ùïú)) :
    p f := by
  refine f.induction_on const id star_id add mul fun h f ‚Ü¶ frequently f ?_
  have := polynomialFunctions.starClosure_topologicalClosure s ‚ñ∏ mem_top (x := f)
  rw [‚Üê SetLike.mem_coe, topologicalClosure_coe, mem_closure_iff_frequently] at this
  exact this.mp <| .of_forall h

/-- Continuous algebra homomorphisms from `C(s, ‚Ñù)` into an `‚Ñù`-algebra `A` which agree
at `X : ùïú[X]` (interpreted as a continuous map) are, in fact, equal. -/
@[ext (iff := false)]
theorem ContinuousMap.algHom_ext_map_X {A : Type*} [Semiring A]
    [Algebra ‚Ñù A] [TopologicalSpace A] [T2Space A] {s : Set ‚Ñù} [CompactSpace s]
    {œÜ œà : C(s, ‚Ñù) ‚Üí‚Çê[‚Ñù] A} (hœÜ : Continuous œÜ) (hœà : Continuous œà)
    (h : œÜ (toContinuousMapOnAlgHom s X) = œà (toContinuousMapOnAlgHom s X)) : œÜ = œà := by
  suffices (‚ä§ : Subalgebra ‚Ñù C(s, ‚Ñù)) ‚â§ AlgHom.equalizer œÜ œà from
    AlgHom.ext fun x => this (by trivial)
  rw [‚Üê polynomialFunctions.topologicalClosure s]
  exact Subalgebra.topologicalClosure_minimal (polynomialFunctions s)
    (polynomialFunctions.le_equalizer s œÜ œà h) (isClosed_eq hœÜ hœà)

/-- Continuous star algebra homomorphisms from `C(s, ùïú)` into a star `ùïú`-algebra `A` which agree
at `X : ùïú[X]` (interpreted as a continuous map) are, in fact, equal. -/
@[ext (iff := false)]
theorem ContinuousMap.starAlgHom_ext_map_X {ùïú A : Type*} [RCLike ùïú] [Ring A] [StarRing A]
    [Algebra ùïú A] [TopologicalSpace A] [T2Space A] {s : Set ùïú} [CompactSpace s]
    {œÜ œà : C(s, ùïú) ‚Üí‚ãÜ‚Çê[ùïú] A} (hœÜ : Continuous œÜ) (hœà : Continuous œà)
    (h : œÜ (toContinuousMapOnAlgHom s X) = œà (toContinuousMapOnAlgHom s X)) : œÜ = œà := by
  suffices (‚ä§ : StarSubalgebra ùïú C(s, ùïú)) ‚â§ StarAlgHom.equalizer œÜ œà from
    StarAlgHom.ext fun x => this mem_top
  rw [‚Üê polynomialFunctions.starClosure_topologicalClosure s]
  exact StarSubalgebra.topologicalClosure_minimal
    (polynomialFunctions.starClosure_le_equalizer s œÜ œà h) (isClosed_eq hœÜ hœà)

end PolynomialFunctions

/-! ### Continuous maps sending zero to zero -/

section ContinuousMapZero

variable {ùïú : Type*} [RCLike ùïú]
open NonUnitalStarAlgebra Submodule

namespace ContinuousMap

lemma adjoin_id_eq_span_one_union (s : Set ùïú) :
    ((StarAlgebra.adjoin ùïú {(restrict s (.id ùïú) : C(s, ùïú))}) : Set C(s, ùïú)) =
      span ùïú ({(1 : C(s, ùïú))} ‚à™ (adjoin ùïú {(restrict s (.id ùïú) : C(s, ùïú))})) := by
  ext x
  rw [SetLike.mem_coe, SetLike.mem_coe, ‚Üê StarAlgebra.adjoin_nonUnitalStarSubalgebra,
    ‚Üê StarSubalgebra.mem_toSubalgebra, ‚Üê Subalgebra.mem_toSubmodule,
    StarAlgebra.adjoin_nonUnitalStarSubalgebra_eq_span, span_union, span_eq_toSubmodule]

open Pointwise in
lemma adjoin_id_eq_span_one_add (s : Set ùïú) :
    ((StarAlgebra.adjoin ùïú {(restrict s (.id ùïú) : C(s, ùïú))}) : Set C(s, ùïú)) =
      (span ùïú {(1 : C(s, ùïú))} : Set C(s, ùïú)) + (adjoin ùïú {(restrict s (.id ùïú) : C(s, ùïú))}) := by
  ext x
  rw [SetLike.mem_coe, ‚Üê StarAlgebra.adjoin_nonUnitalStarSubalgebra,
    ‚Üê StarSubalgebra.mem_toSubalgebra, ‚Üê Subalgebra.mem_toSubmodule,
    StarAlgebra.adjoin_nonUnitalStarSubalgebra_eq_span, mem_sup]
  simp [Set.mem_add]

lemma nonUnitalStarAlgebraAdjoin_id_subset_ker_evalStarAlgHom {s : Set ùïú} (h0 : 0 ‚àà s) :
    (adjoin ùïú {restrict s (.id ùïú)} : Set C(s, ùïú)) ‚äÜ
      RingHom.ker (evalStarAlgHom ùïú ùïú (‚ü®0, h0‚ü© : s)) := by
  intro f hf
  induction hf using adjoin_induction with
  | mem f hf =>
    obtain rfl := Set.mem_singleton_iff.mp hf
    rfl
  | add f g _ _ hf hg => exact add_mem hf hg
  | zero => exact zero_mem _
  | mul f g _ _ _ hg => exact Ideal.mul_mem_left _ f hg
  | smul r f _ hf =>
    rw [SetLike.mem_coe, RingHom.mem_ker] at hf ‚ä¢
    rw [map_smul, hf, smul_zero]
  | star f _ hf =>
    rw [SetLike.mem_coe, RingHom.mem_ker] at hf ‚ä¢
    rw [map_star, hf, star_zero]

lemma ker_evalStarAlgHom_inter_adjoin_id (s : Set ùïú) (h0 : 0 ‚àà s) :
    (StarAlgebra.adjoin ùïú {restrict s (.id ùïú)} : Set C(s, ùïú)) ‚à©
      RingHom.ker (evalStarAlgHom ùïú ùïú (‚ü®0, h0‚ü© : s)) = adjoin ùïú {restrict s (.id ùïú)} := by
  ext f
  constructor
  ¬∑ rintro ‚ü®hf‚ÇÅ, hf‚ÇÇ‚ü©
    rw [SetLike.mem_coe] at hf‚ÇÇ ‚ä¢
    simp_rw [adjoin_id_eq_span_one_add, Set.mem_add, SetLike.mem_coe, mem_span_singleton] at hf‚ÇÅ
    obtain ‚ü®-, ‚ü®r, rfl‚ü©, f, hf, rfl‚ü© := hf‚ÇÅ
    have := nonUnitalStarAlgebraAdjoin_id_subset_ker_evalStarAlgHom h0 hf
    simp only [SetLike.mem_coe, RingHom.mem_ker, evalStarAlgHom_apply] at hf‚ÇÇ this
    rw [add_apply, this, add_zero, smul_apply, one_apply, smul_eq_mul, mul_one] at hf‚ÇÇ
    rwa [hf‚ÇÇ, zero_smul, zero_add]
  ¬∑ simp only [Set.mem_inter_iff, SetLike.mem_coe]
    refine fun hf ‚Ü¶ ‚ü®?_, nonUnitalStarAlgebraAdjoin_id_subset_ker_evalStarAlgHom h0 hf‚ü©
    exact adjoin_le_starAlgebra_adjoin _ _ hf

-- the statement should be in terms of non unital subalgebras, but we lack API
open RingHom Filter Topology in
theorem AlgHom.closure_ker_inter {F S K A : Type*} [CommRing K] [Ring A] [Algebra K A]
    [TopologicalSpace K] [T1Space K] [TopologicalSpace A] [ContinuousSub A] [ContinuousSMul K A]
    [FunLike F A K] [AlgHomClass F K A K] [SetLike S A] [OneMemClass S A] [AddSubgroupClass S A]
    [SMulMemClass S K A] (œÜ : F) (hœÜ : Continuous œÜ) (s : S) :
    closure (s ‚à© RingHom.ker œÜ) = closure s ‚à© (ker œÜ : Set A) := by
  refine subset_antisymm ?_ ?_
  ¬∑ simpa only [ker_eq, (isClosed_singleton.preimage hœÜ).closure_eq]
      using closure_inter_subset_inter_closure s (ker œÜ : Set A)
  ¬∑ intro x ‚ü®hxs, (hxœÜ : œÜ x = 0)‚ü©
    rw [mem_closure_iff_clusterPt, ClusterPt] at hxs
    have : Tendsto (fun y ‚Ü¶ y - œÜ y ‚Ä¢ 1) (ùìù x ‚äì ùìü s) (ùìù x) := by
      conv => congr; rfl; rfl; rw [‚Üê sub_zero x, ‚Üê zero_smul K 1, ‚Üê hxœÜ]
      exact Filter.tendsto_inf_left (Continuous.tendsto (by fun_prop) x)
    refine mem_closure_of_tendsto this <| eventually_inf_principal.mpr ?_
    filter_upwards [] with g hg using
      ‚ü®sub_mem hg (SMulMemClass.smul_mem _ <| one_mem _), by simp [RingHom.mem_ker]‚ü©

lemma ker_evalStarAlgHom_eq_closure_adjoin_id (s : Set ùïú) (h0 : 0 ‚àà s) [CompactSpace s] :
    (RingHom.ker (evalStarAlgHom ùïú ùïú (‚ü®0, h0‚ü© : s)) : Set C(s, ùïú)) =
      closure (adjoin ùïú {(restrict s (.id ùïú))}) := by
  rw [‚Üê ker_evalStarAlgHom_inter_adjoin_id s h0,
    AlgHom.closure_ker_inter (œÜ := evalStarAlgHom ùïú ùïú (X := s) ‚ü®0, h0‚ü©) (continuous_eval_const _) _]
  convert (Set.univ_inter _).symm
  rw [‚Üê Polynomial.toContinuousMapOn_X_eq_restrict_id, ‚Üê Polynomial.toContinuousMapOnAlgHom_apply,
    ‚Üê polynomialFunctions.starClosure_eq_adjoin_X s]
  congrm(($(polynomialFunctions.starClosure_topologicalClosure s) : Set C(s, ùïú)))

end ContinuousMap

open scoped ContinuousMapZero

/-- If `s : Set ùïú` with `RCLike ùïú` is compact and contains `0`, then the non-unital star subalgebra
generated by the identity function in `C(s, ùïú)‚ÇÄ` is dense. This can be seen as a version of the
Weierstrass approximation theorem. -/
lemma ContinuousMapZero.adjoin_id_dense {s : Set ùïú} [Zero s] (h0 : ((0 : s) : ùïú) = 0)
    [CompactSpace s] : Dense (adjoin ùïú {(.id h0 : C(s, ùïú)‚ÇÄ)} : Set C(s, ùïú)‚ÇÄ) := by
  have h0' : 0 ‚àà s := h0 ‚ñ∏ (0 : s).property
  rw [dense_iff_closure_eq,
    ‚Üê isClosedEmbedding_toContinuousMap.injective.preimage_image (closure _),
    ‚Üê isClosedEmbedding_toContinuousMap.closure_image_eq, ‚Üê coe_toContinuousMapHom,
    ‚Üê NonUnitalStarSubalgebra.coe_map, NonUnitalStarAlgHom.map_adjoin_singleton,
    toContinuousMapHom_apply, toContinuousMap_id h0,
    ‚Üê ContinuousMap.ker_evalStarAlgHom_eq_closure_adjoin_id s h0']
  apply Set.eq_univ_of_forall fun f ‚Ü¶ ?_
  simp only [Set.mem_preimage, toContinuousMapHom_apply, SetLike.mem_coe, RingHom.mem_ker,
    ContinuousMap.evalStarAlgHom_apply, ContinuousMap.coe_coe]
  rw [show ‚ü®0, h0'‚ü© = (0 : s) by ext; exact h0.symm, map_zero f]

/-- An induction principle for `C(s, ùïú)‚ÇÄ`. -/
@[elab_as_elim]
lemma ContinuousMapZero.induction_on {s : Set ùïú} [Zero s] (h0 : ((0 : s) : ùïú) = 0)
    {p : C(s, ùïú)‚ÇÄ ‚Üí Prop} (zero : p 0) (id : p (.id h0)) (star_id : p (star (.id h0)))
    (add : ‚àÄ f g, p f ‚Üí p g ‚Üí p (f + g)) (mul : ‚àÄ f g, p f ‚Üí p g ‚Üí p (f * g))
    (smul : ‚àÄ (r : ùïú) f, p f ‚Üí p (r ‚Ä¢ f))
    (closure : (‚àÄ f ‚àà adjoin ùïú {(.id h0 : C(s, ùïú)‚ÇÄ)}, p f) ‚Üí ‚àÄ f, p f) (f : C(s, ùïú)‚ÇÄ) :
    p f := by
  refine closure (fun f hf => ?_) f
  induction hf using NonUnitalAlgebra.adjoin_induction with
  | mem f hf =>
    simp only [Set.mem_union, Set.mem_singleton_iff, Set.mem_star] at hf
    rw [star_eq_iff_star_eq, eq_comm (b := f)] at hf
    obtain (rfl | rfl) := hf
    all_goals assumption
  | zero => exact zero
  | add _ _ _ _ hf hg => exact add _ _ hf hg
  | mul _ _ _ _ hf hg => exact mul _ _ hf hg
  | smul _ _ _ hf => exact smul _ _ hf

open Topology in
@[elab_as_elim]
theorem ContinuousMapZero.induction_on_of_compact {s : Set ùïú} [Zero s] (h0 : ((0 : s) : ùïú) = 0)
    [CompactSpace s] {p : C(s, ùïú)‚ÇÄ ‚Üí Prop} (zero : p 0) (id : p (.id h0))
    (star_id : p (star (.id h0))) (add : ‚àÄ f g, p f ‚Üí p g ‚Üí p (f + g))
    (mul : ‚àÄ f g, p f ‚Üí p g ‚Üí p (f * g)) (smul : ‚àÄ (r : ùïú) f, p f ‚Üí p (r ‚Ä¢ f))
    (frequently : ‚àÄ f, (‚àÉ·∂† g in ùìù f, p g) ‚Üí p f) (f : C(s, ùïú)‚ÇÄ) :
    p f := by
  refine f.induction_on h0 zero id star_id add mul smul fun h f ‚Ü¶ frequently f ?_
  have := (ContinuousMapZero.adjoin_id_dense h0).closure_eq ‚ñ∏ Set.mem_univ (x := f)
  exact mem_closure_iff_frequently.mp this |>.mp <| .of_forall h

lemma ContinuousMapZero.nonUnitalStarAlgHom_apply_mul_eq_zero {ùïú A : Type*}
    [RCLike ùïú] [NonUnitalSemiring A] [Star A] [TopologicalSpace A] [ContinuousMul A]
    [T2Space A] [DistribMulAction ùïú A] [IsScalarTower ùïú A A] {s : Set ùïú} [Zero s] [CompactSpace s]
    (h0 : (0 : s) = (0 : ùïú)) (œÜ : C(s, ùïú)‚ÇÄ ‚Üí‚ãÜ‚Çô‚Çê[ùïú] A) (a : A) (hmul_id : œÜ (.id h0) * a = 0)
    (hmul_star_id : œÜ (star (.id h0)) * a = 0) (hœÜ : Continuous œÜ) (f : C(s, ùïú)‚ÇÄ) :
    œÜ f * a = 0 := by
  induction f using ContinuousMapZero.induction_on_of_compact h0 with
  | zero => simp [map_zero]
  | id => exact hmul_id
  | star_id => exact hmul_star_id
  | add _ _ h‚ÇÅ h‚ÇÇ => simp only [map_add, add_mul, h‚ÇÅ, h‚ÇÇ, zero_add]
  | mul _ _ _ h => simp only [map_mul, mul_assoc, h, mul_zero]
  | smul _ _ h => rw [map_smul, smul_mul_assoc, h, smul_zero]
  | frequently f h => exact h.mem_of_closed <| isClosed_eq (by fun_prop) continuous_zero

lemma ContinuousMapZero.mul_nonUnitalStarAlgHom_apply_eq_zero {ùïú A : Type*}
    [RCLike ùïú] [NonUnitalSemiring A] [Star A] [TopologicalSpace A] [ContinuousMul A]
    [T2Space A] [DistribMulAction ùïú A] [SMulCommClass ùïú A A] {s : Set ùïú} [Zero s] [CompactSpace s]
    (h0 : (0 : s) = (0 : ùïú)) (œÜ : C(s, ùïú)‚ÇÄ ‚Üí‚ãÜ‚Çô‚Çê[ùïú] A) (a : A) (hmul_id : a * œÜ (.id h0) = 0)
    (hmul_star_id : a * œÜ (star (.id h0)) = 0) (hœÜ : Continuous œÜ) (f : C(s, ùïú)‚ÇÄ) :
    a * œÜ f = 0 := by
  induction f using ContinuousMapZero.induction_on_of_compact h0 with
  | zero => simp [map_zero]
  | id => exact hmul_id
  | star_id => exact hmul_star_id
  | add _ _ h‚ÇÅ h‚ÇÇ => simp only [map_add, mul_add, h‚ÇÅ, h‚ÇÇ, zero_add]
  | mul _ _ h _ => simp only [map_mul, ‚Üê mul_assoc, h, zero_mul]
  | smul _ _ h => rw [map_smul, mul_smul_comm, h, smul_zero]
  | frequently f h => exact h.mem_of_closed <| isClosed_eq (by fun_prop) continuous_zero

end ContinuousMapZero
