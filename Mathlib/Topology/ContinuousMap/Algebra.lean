/-
Copyright (c) 2019 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Nicol√≤ Cavalleri
-/
import Mathlib.Algebra.Algebra.Pi
import Mathlib.Algebra.Algebra.Subalgebra.Basic
import Mathlib.Tactic.FieldSimp
import Mathlib.Topology.Algebra.InfiniteSum.Basic
import Mathlib.Topology.Algebra.Module.LinearMap
import Mathlib.Topology.Algebra.Ring.Basic
import Mathlib.Topology.UniformSpace.CompactConvergence

/-!
# Algebraic structures over continuous functions

In this file we define instances of algebraic structures over the type `ContinuousMap Œ± Œ≤`
(denoted `C(Œ±, Œ≤)`) of **bundled** continuous maps from `Œ±` to `Œ≤`. For example, `C(Œ±, Œ≤)`
is a group when `Œ≤` is a group, a ring when `Œ≤` is a ring, etc.

For each type of algebraic structure, we also define an appropriate subobject of `Œ± ‚Üí Œ≤`
with carrier `{ f : Œ± ‚Üí Œ≤ | Continuous f }`. For example, when `Œ≤` is a group, a subgroup
`continuousSubgroup Œ± Œ≤` of `Œ± ‚Üí Œ≤` is constructed with carrier `{ f : Œ± ‚Üí Œ≤ | Continuous f }`.

Note that, rather than using the derived algebraic structures on these subobjects
(for example, when `Œ≤` is a group, the derived group structure on `continuousSubgroup Œ± Œ≤`),
one should use `C(Œ±, Œ≤)` with the appropriate instance of the structure.
-/

assert_not_exists StoneCech

--attribute [elab_without_expected_type] Continuous.comp

namespace ContinuousFunctions

variable {Œ± : Type*} {Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤]
variable {f g : { f : Œ± ‚Üí Œ≤ | Continuous f }}

instance : CoeFun { f : Œ± ‚Üí Œ≤ | Continuous f } fun _ => Œ± ‚Üí Œ≤ :=
  ‚ü®Subtype.val‚ü©

end ContinuousFunctions

namespace ContinuousMap

variable {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*}
variable [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [TopologicalSpace Œ≥]

/-! ### `mul` and `add` -/

@[to_additive]
instance instMul [Mul Œ≤] [ContinuousMul Œ≤] : Mul C(Œ±, Œ≤) :=
  ‚ü®fun f g => ‚ü®f * g, continuous_mul.comp (f.continuous.prodMk g.continuous :)‚ü©‚ü©

@[to_additive (attr := norm_cast, simp)]
theorem coe_mul [Mul Œ≤] [ContinuousMul Œ≤] (f g : C(Œ±, Œ≤)) : ‚áë(f * g) = f * g :=
  rfl

@[to_additive (attr := simp)]
theorem mul_apply [Mul Œ≤] [ContinuousMul Œ≤] (f g : C(Œ±, Œ≤)) (x : Œ±) : (f * g) x = f x * g x :=
  rfl

@[to_additive (attr := simp)]
theorem mul_comp [Mul Œ≥] [ContinuousMul Œ≥] (f‚ÇÅ f‚ÇÇ : C(Œ≤, Œ≥)) (g : C(Œ±, Œ≤)) :
    (f‚ÇÅ * f‚ÇÇ).comp g = f‚ÇÅ.comp g * f‚ÇÇ.comp g :=
  rfl

/-! ### `one` -/

@[to_additive]
instance [One Œ≤] : One C(Œ±, Œ≤) :=
  ‚ü®const Œ± 1‚ü©

@[to_additive (attr := norm_cast, simp)]
theorem coe_one [One Œ≤] : ‚áë(1 : C(Œ±, Œ≤)) = 1 :=
  rfl

@[to_additive (attr := simp)]
theorem one_apply [One Œ≤] (x : Œ±) : (1 : C(Œ±, Œ≤)) x = 1 :=
  rfl

@[to_additive (attr := simp)]
theorem one_comp [One Œ≥] (g : C(Œ±, Œ≤)) : (1 : C(Œ≤, Œ≥)).comp g = 1 :=
  rfl

@[to_additive (attr := simp)]
theorem comp_one [One Œ≤] (g : C(Œ≤, Œ≥)) : g.comp (1 : C(Œ±, Œ≤)) = const Œ± (g 1) := rfl

@[to_additive (attr := simp)]
theorem const_one [One Œ≤] : const Œ± (1 : Œ≤) = 1 := rfl

/-! ### `Nat.cast` -/

instance [NatCast Œ≤] : NatCast C(Œ±, Œ≤) :=
  ‚ü®fun n => ContinuousMap.const _ n‚ü©

@[simp, norm_cast]
theorem coe_natCast [NatCast Œ≤] (n : ‚Ñï) : ((n : C(Œ±, Œ≤)) : Œ± ‚Üí Œ≤) = n :=
  rfl

@[simp]
theorem natCast_apply [NatCast Œ≤] (n : ‚Ñï) (x : Œ±) : (n : C(Œ±, Œ≤)) x = n :=
  rfl

/-! ### `Int.cast` -/

instance [IntCast Œ≤] : IntCast C(Œ±, Œ≤) :=
  ‚ü®fun n => ContinuousMap.const _ n‚ü©

@[simp, norm_cast]
theorem coe_intCast [IntCast Œ≤] (n : ‚Ñ§) : ((n : C(Œ±, Œ≤)) : Œ± ‚Üí Œ≤) = n :=
  rfl

@[simp]
theorem intCast_apply [IntCast Œ≤] (n : ‚Ñ§) (x : Œ±) : (n : C(Œ±, Œ≤)) x = n :=
  rfl

/-! ### `nsmul` and `pow` -/

instance instNSMul [AddMonoid Œ≤] [ContinuousAdd Œ≤] : SMul ‚Ñï C(Œ±, Œ≤) :=
  ‚ü®fun n f => ‚ü®n ‚Ä¢ ‚áëf, f.continuous.nsmul n‚ü©‚ü©

@[to_additive existing]
instance instPow [Monoid Œ≤] [ContinuousMul Œ≤] : Pow C(Œ±, Œ≤) ‚Ñï :=
  ‚ü®fun f n => ‚ü®(‚áëf) ^ n, f.continuous.pow n‚ü©‚ü©

@[to_additive (attr := norm_cast) (reorder := 7 8)]
theorem coe_pow [Monoid Œ≤] [ContinuousMul Œ≤] (f : C(Œ±, Œ≤)) (n : ‚Ñï) : ‚áë(f ^ n) = (‚áëf) ^ n :=
  rfl

@[to_additive (attr := norm_cast)]
theorem pow_apply [Monoid Œ≤] [ContinuousMul Œ≤] (f : C(Œ±, Œ≤)) (n : ‚Ñï) (x : Œ±) :
    (f ^ n) x = f x ^ n :=
  rfl

-- Don't make auto-generated `coe_nsmul` and `nsmul_apply` simp, as the linter complains they're
-- redundant w.r.t. `coe_smul`
attribute [simp] coe_pow pow_apply

@[to_additive]
theorem pow_comp [Monoid Œ≥] [ContinuousMul Œ≥] (f : C(Œ≤, Œ≥)) (n : ‚Ñï) (g : C(Œ±, Œ≤)) :
    (f ^ n).comp g = f.comp g ^ n :=
  rfl

-- Don't make `nsmul_comp` simp as the linter complains it's redundant w.r.t. `smul_comp`
attribute [simp] pow_comp

/-! ### `inv` and `neg` -/

@[to_additive]
instance [Inv Œ≤] [ContinuousInv Œ≤] : Inv C(Œ±, Œ≤) where inv f := ‚ü®f‚Åª¬π, f.continuous.inv‚ü©

@[to_additive (attr := simp)]
theorem coe_inv [Inv Œ≤] [ContinuousInv Œ≤] (f : C(Œ±, Œ≤)) : ‚áëf‚Åª¬π = (‚áëf)‚Åª¬π :=
  rfl

@[to_additive (attr := simp)]
theorem inv_apply [Inv Œ≤] [ContinuousInv Œ≤] (f : C(Œ±, Œ≤)) (x : Œ±) : f‚Åª¬π x = (f x)‚Åª¬π :=
  rfl

@[to_additive (attr := simp)]
theorem inv_comp [Inv Œ≥] [ContinuousInv Œ≥] (f : C(Œ≤, Œ≥)) (g : C(Œ±, Œ≤)) :
    f‚Åª¬π.comp g = (f.comp g)‚Åª¬π :=
  rfl

/-! ### `div` and `sub` -/

@[to_additive]
instance [Div Œ≤] [ContinuousDiv Œ≤] : Div C(Œ±, Œ≤) where
  div f g := ‚ü®f / g, f.continuous.div' g.continuous‚ü©

@[to_additive (attr := norm_cast, simp)]
theorem coe_div [Div Œ≤] [ContinuousDiv Œ≤] (f g : C(Œ±, Œ≤)) : ‚áë(f / g) = f / g :=
  rfl

@[to_additive (attr := simp)]
theorem div_apply [Div Œ≤] [ContinuousDiv Œ≤] (f g : C(Œ±, Œ≤)) (x : Œ±) : (f / g) x = f x / g x :=
  rfl

@[to_additive (attr := simp)]
theorem div_comp [Div Œ≥] [ContinuousDiv Œ≥] (f g : C(Œ≤, Œ≥)) (h : C(Œ±, Œ≤)) :
    (f / g).comp h = f.comp h / g.comp h :=
  rfl

/-! ### `zpow` and `zsmul` -/

instance instZSMul [AddGroup Œ≤] [IsTopologicalAddGroup Œ≤] : SMul ‚Ñ§ C(Œ±, Œ≤) where
  smul z f := ‚ü®z ‚Ä¢ ‚áëf, f.continuous.zsmul z‚ü©

@[to_additive existing]
instance instZPow [Group Œ≤] [IsTopologicalGroup Œ≤] : Pow C(Œ±, Œ≤) ‚Ñ§ where
  pow f z := ‚ü®(‚áëf) ^ z, f.continuous.zpow z‚ü©

@[to_additive (attr := norm_cast) (reorder := 7 8)]
theorem coe_zpow [Group Œ≤] [IsTopologicalGroup Œ≤] (f : C(Œ±, Œ≤)) (z : ‚Ñ§) : ‚áë(f ^ z) = (‚áëf) ^ z :=
  rfl

@[to_additive]
theorem zpow_apply [Group Œ≤] [IsTopologicalGroup Œ≤] (f : C(Œ±, Œ≤)) (z : ‚Ñ§) (x : Œ±) :
    (f ^ z) x = f x ^ z :=
  rfl

-- Don't make auto-generated `coe_zsmul` and `zsmul_apply` simp as the linter complains they're
-- redundant w.r.t. `coe_smul`
attribute [simp] coe_zpow zpow_apply

@[to_additive]
theorem zpow_comp [Group Œ≥] [IsTopologicalGroup Œ≥] (f : C(Œ≤, Œ≥)) (z : ‚Ñ§) (g : C(Œ±, Œ≤)) :
    (f ^ z).comp g = f.comp g ^ z :=
  rfl

-- Don't make `zsmul_comp` simp as the linter complains it's redundant w.r.t. `smul_comp`
attribute [simp] zpow_comp

end ContinuousMap

section GroupStructure

/-!
### Group structure

In this section we show that continuous functions valued in a topological group inherit
the structure of a group.
-/


section Subtype

/-- The `Submonoid` of continuous maps `Œ± ‚Üí Œ≤`. -/
@[to_additive /-- The `AddSubmonoid` of continuous maps `Œ± ‚Üí Œ≤`. -/]
def continuousSubmonoid (Œ± : Type*) (Œ≤ : Type*) [TopologicalSpace Œ±] [TopologicalSpace Œ≤]
    [MulOneClass Œ≤] [ContinuousMul Œ≤] : Submonoid (Œ± ‚Üí Œ≤) where
  carrier := { f : Œ± ‚Üí Œ≤ | Continuous f }
  one_mem' := @continuous_const _ _ _ _ 1
  mul_mem' fc gc := fc.mul gc

/-- The subgroup of continuous maps `Œ± ‚Üí Œ≤`. -/
@[to_additive /-- The `AddSubgroup` of continuous maps `Œ± ‚Üí Œ≤`. -/]
def continuousSubgroup (Œ± : Type*) (Œ≤ : Type*) [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [Group Œ≤]
    [IsTopologicalGroup Œ≤] : Subgroup (Œ± ‚Üí Œ≤) :=
  { continuousSubmonoid Œ± Œ≤ with inv_mem' := fun fc => Continuous.inv fc }

end Subtype

namespace ContinuousMap

variable {Œ± Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤]

@[to_additive]
instance [Semigroup Œ≤] [ContinuousMul Œ≤] : Semigroup C(Œ±, Œ≤) :=
  coe_injective.semigroup _ coe_mul

@[to_additive]
instance [CommSemigroup Œ≤] [ContinuousMul Œ≤] : CommSemigroup C(Œ±, Œ≤) :=
  coe_injective.commSemigroup _ coe_mul

@[to_additive]
instance [MulOneClass Œ≤] [ContinuousMul Œ≤] : MulOneClass C(Œ±, Œ≤) :=
  coe_injective.mulOneClass _ coe_one coe_mul

instance [MulZeroClass Œ≤] [ContinuousMul Œ≤] : MulZeroClass C(Œ±, Œ≤) :=
  coe_injective.mulZeroClass _ coe_zero coe_mul

instance [SemigroupWithZero Œ≤] [ContinuousMul Œ≤] : SemigroupWithZero C(Œ±, Œ≤) :=
  coe_injective.semigroupWithZero _ coe_zero coe_mul

@[to_additive]
instance [Monoid Œ≤] [ContinuousMul Œ≤] : Monoid C(Œ±, Œ≤) :=
  coe_injective.monoid _ coe_one coe_mul coe_pow

instance [MonoidWithZero Œ≤] [ContinuousMul Œ≤] : MonoidWithZero C(Œ±, Œ≤) :=
  coe_injective.monoidWithZero _ coe_zero coe_one coe_mul coe_pow

@[to_additive]
instance [CommMonoid Œ≤] [ContinuousMul Œ≤] : CommMonoid C(Œ±, Œ≤) :=
  coe_injective.commMonoid _ coe_one coe_mul coe_pow

instance [CommMonoidWithZero Œ≤] [ContinuousMul Œ≤] : CommMonoidWithZero C(Œ±, Œ≤) :=
  coe_injective.commMonoidWithZero _ coe_zero coe_one coe_mul coe_pow

@[to_additive]
instance [LocallyCompactSpace Œ±] [Mul Œ≤] [ContinuousMul Œ≤] : ContinuousMul C(Œ±, Œ≤) :=
  ‚ü®by
    refine continuous_of_continuous_uncurry _ ?_
    have h1 : Continuous fun x : (C(Œ±, Œ≤) √ó C(Œ±, Œ≤)) √ó Œ± => x.fst.fst x.snd :=
      continuous_eval.comp (continuous_fst.prodMap continuous_id)
    have h2 : Continuous fun x : (C(Œ±, Œ≤) √ó C(Œ±, Œ≤)) √ó Œ± => x.fst.snd x.snd :=
      continuous_eval.comp (continuous_snd.prodMap continuous_id)
    exact h1.mul h2‚ü©

/-- Coercion to a function as a `MonoidHom`. Similar to `MonoidHom.coeFn`. -/
@[to_additive (attr := simps)
  /-- Coercion to a function as an `AddMonoidHom`. Similar to `AddMonoidHom.coeFn`. -/]
def coeFnMonoidHom [Monoid Œ≤] [ContinuousMul Œ≤] : C(Œ±, Œ≤) ‚Üí* Œ± ‚Üí Œ≤ where
  toFun f := f
  map_one' := coe_one
  map_mul' := coe_mul

variable (Œ±) in
/-- Composition on the left by a (continuous) homomorphism of topological monoids, as a
`MonoidHom`. Similar to `MonoidHom.compLeft`. -/
@[to_additive (attr := simps)
/-- Composition on the left by a (continuous) homomorphism of topological `AddMonoid`s, as an
`AddMonoidHom`. Similar to `AddMonoidHom.comp_left`. -/]
protected def _root_.MonoidHom.compLeftContinuous {Œ≥ : Type*} [Monoid Œ≤] [ContinuousMul Œ≤]
    [TopologicalSpace Œ≥] [Monoid Œ≥] [ContinuousMul Œ≥] (g : Œ≤ ‚Üí* Œ≥) (hg : Continuous g) :
    C(Œ±, Œ≤) ‚Üí* C(Œ±, Œ≥) where
  toFun f := (‚ü®g, hg‚ü© : C(Œ≤, Œ≥)).comp f
  map_one' := ext fun _ => g.map_one
  map_mul' _ _ := ext fun _ => g.map_mul _ _

/-- Composition on the right as a `MonoidHom`. Similar to `MonoidHom.compHom'`. -/
@[to_additive (attr := simps)
      /-- Composition on the right as an `AddMonoidHom`. Similar to `AddMonoidHom.compHom'`. -/]
def compMonoidHom' {Œ≥ : Type*} [TopologicalSpace Œ≥] [MulOneClass Œ≥] [ContinuousMul Œ≥]
    (g : C(Œ±, Œ≤)) : C(Œ≤, Œ≥) ‚Üí* C(Œ±, Œ≥) where
  toFun f := f.comp g
  map_one' := one_comp g
  map_mul' f‚ÇÅ f‚ÇÇ := mul_comp f‚ÇÅ f‚ÇÇ g

@[to_additive (attr := simp)]
theorem coe_prod [CommMonoid Œ≤] [ContinuousMul Œ≤] {Œπ : Type*} (s : Finset Œπ) (f : Œπ ‚Üí C(Œ±, Œ≤)) :
    ‚áë(‚àè i ‚àà s, f i) = ‚àè i ‚àà s, (f i : Œ± ‚Üí Œ≤) :=
  map_prod coeFnMonoidHom f s

@[to_additive]
theorem prod_apply [CommMonoid Œ≤] [ContinuousMul Œ≤] {Œπ : Type*} (s : Finset Œπ) (f : Œπ ‚Üí C(Œ±, Œ≤))
    (a : Œ±) : (‚àè i ‚àà s, f i) a = ‚àè i ‚àà s, f i a := by simp

@[to_additive]
instance [Group Œ≤] [IsTopologicalGroup Œ≤] : Group C(Œ±, Œ≤) :=
  coe_injective.group _ coe_one coe_mul coe_inv coe_div coe_pow coe_zpow

@[to_additive]
instance instCommGroupContinuousMap [CommGroup Œ≤] [IsTopologicalGroup Œ≤] : CommGroup C(Œ±, Œ≤) :=
  coe_injective.commGroup _ coe_one coe_mul coe_inv coe_div coe_pow coe_zpow

@[to_additive]
instance [CommGroup Œ≤] [IsTopologicalGroup Œ≤] : IsTopologicalGroup C(Œ±, Œ≤) where
  continuous_mul := by
    letI : UniformSpace Œ≤ := IsTopologicalGroup.toUniformSpace Œ≤
    have : IsUniformGroup Œ≤ := isUniformGroup_of_commGroup
    rw [continuous_iff_continuousAt]
    rintro ‚ü®f, g‚ü©
    rw [ContinuousAt, tendsto_iff_forall_isCompact_tendstoUniformlyOn, nhds_prod_eq]
    exact fun K hK =>
      uniformContinuous_mul.comp_tendstoUniformlyOn
        ((tendsto_iff_forall_isCompact_tendstoUniformlyOn.mp Filter.tendsto_id K hK).prodMk
          (tendsto_iff_forall_isCompact_tendstoUniformlyOn.mp Filter.tendsto_id K hK))
  continuous_inv := by
    letI : UniformSpace Œ≤ := IsTopologicalGroup.toUniformSpace Œ≤
    have : IsUniformGroup Œ≤ := isUniformGroup_of_commGroup
    rw [continuous_iff_continuousAt]
    intro f
    rw [ContinuousAt, tendsto_iff_forall_isCompact_tendstoUniformlyOn]
    exact fun K hK =>
      uniformContinuous_inv.comp_tendstoUniformlyOn
        (tendsto_iff_forall_isCompact_tendstoUniformlyOn.mp Filter.tendsto_id K hK)

/-- If an infinite product of functions in `C(Œ±, Œ≤)` converges to `g`
(for the compact-open topology), then the pointwise product converges to `g x` for all `x ‚àà Œ±`. -/
@[to_additive
  /-- If an infinite sum of functions in `C(Œ±, Œ≤)` converges to `g` (for the compact-open topology),
then the pointwise sum converges to `g x` for all `x ‚àà Œ±`. -/]
theorem hasProd_apply {Œ≥ : Type*} [CommMonoid Œ≤] [ContinuousMul Œ≤]
    {f : Œ≥ ‚Üí C(Œ±, Œ≤)} {g : C(Œ±, Œ≤)} (hf : HasProd f g) (x : Œ±) :
    HasProd (fun i : Œ≥ => f i x) (g x) := by
  let ev : C(Œ±, Œ≤) ‚Üí* Œ≤ := (Pi.evalMonoidHom _ x).comp coeFnMonoidHom
  exact hf.map ev (continuous_eval_const x)

@[to_additive]
theorem multipliable_apply [CommMonoid Œ≤] [ContinuousMul Œ≤] {Œ≥ : Type*} {f : Œ≥ ‚Üí C(Œ±, Œ≤)}
    (hf : Multipliable f) (x : Œ±) : Multipliable fun i : Œ≥ => f i x :=
  (hasProd_apply hf.hasProd x).multipliable

@[to_additive]
theorem tprod_apply [T2Space Œ≤] [CommMonoid Œ≤] [ContinuousMul Œ≤] {Œ≥ : Type*} {f : Œ≥ ‚Üí C(Œ±, Œ≤)}
    (hf : Multipliable f) (x : Œ±) :
    ‚àè' i : Œ≥, f i x = (‚àè' i : Œ≥, f i) x :=
  (hasProd_apply hf.hasProd x).tprod_eq

end ContinuousMap

end GroupStructure

section RingStructure

/-!
### Ring structure

In this section we show that continuous functions valued in a topological semiring `R` inherit
the structure of a ring.
-/


section Subtype

/-- The subsemiring of continuous maps `Œ± ‚Üí Œ≤`. -/
def continuousSubsemiring (Œ± : Type*) (R : Type*) [TopologicalSpace Œ±] [TopologicalSpace R]
    [NonAssocSemiring R] [IsTopologicalSemiring R] : Subsemiring (Œ± ‚Üí R) :=
  { continuousAddSubmonoid Œ± R, continuousSubmonoid Œ± R with }

/-- The subring of continuous maps `Œ± ‚Üí Œ≤`. -/
def continuousSubring (Œ± : Type*) (R : Type*) [TopologicalSpace Œ±] [TopologicalSpace R] [Ring R]
    [IsTopologicalRing R] : Subring (Œ± ‚Üí R) :=
  { continuousAddSubgroup Œ± R, continuousSubsemiring Œ± R with }

end Subtype

namespace ContinuousMap

instance {Œ± : Type*} {Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤]
    [NonUnitalNonAssocSemiring Œ≤] [IsTopologicalSemiring Œ≤] : NonUnitalNonAssocSemiring C(Œ±, Œ≤) :=
  coe_injective.nonUnitalNonAssocSemiring _ coe_zero coe_add coe_mul coe_nsmul

instance {Œ± : Type*} {Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [NonUnitalSemiring Œ≤]
    [IsTopologicalSemiring Œ≤] : NonUnitalSemiring C(Œ±, Œ≤) :=
  coe_injective.nonUnitalSemiring _ coe_zero coe_add coe_mul coe_nsmul

instance {Œ± : Type*} {Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [AddMonoidWithOne Œ≤]
    [ContinuousAdd Œ≤] : AddMonoidWithOne C(Œ±, Œ≤) :=
  coe_injective.addMonoidWithOne _ coe_zero coe_one coe_add coe_nsmul coe_natCast

instance {Œ± : Type*} {Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [NonAssocSemiring Œ≤]
    [IsTopologicalSemiring Œ≤] : NonAssocSemiring C(Œ±, Œ≤) :=
  coe_injective.nonAssocSemiring _ coe_zero coe_one coe_add coe_mul coe_nsmul coe_natCast

instance {Œ± : Type*} {Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [Semiring Œ≤]
    [IsTopologicalSemiring Œ≤] : Semiring C(Œ±, Œ≤) :=
  coe_injective.semiring _ coe_zero coe_one coe_add coe_mul coe_nsmul coe_pow coe_natCast

instance {Œ± : Type*} {Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤]
    [NonUnitalNonAssocRing Œ≤] [IsTopologicalRing Œ≤] : NonUnitalNonAssocRing C(Œ±, Œ≤) :=
  coe_injective.nonUnitalNonAssocRing _ coe_zero coe_add coe_mul coe_neg coe_sub coe_nsmul coe_zsmul

instance {Œ± : Type*} {Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [NonUnitalRing Œ≤]
    [IsTopologicalRing Œ≤] : NonUnitalRing C(Œ±, Œ≤) :=
  coe_injective.nonUnitalRing _ coe_zero coe_add coe_mul coe_neg coe_sub coe_nsmul coe_zsmul

instance {Œ± : Type*} {Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [NonAssocRing Œ≤]
    [IsTopologicalRing Œ≤] : NonAssocRing C(Œ±, Œ≤) :=
  coe_injective.nonAssocRing _ coe_zero coe_one coe_add coe_mul coe_neg coe_sub coe_nsmul coe_zsmul
    coe_natCast coe_intCast

instance instRing {Œ± : Type*} {Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [Ring Œ≤]
    [IsTopologicalRing Œ≤] : Ring C(Œ±, Œ≤) :=
  coe_injective.ring _ coe_zero coe_one coe_add coe_mul coe_neg coe_sub coe_nsmul coe_zsmul coe_pow
    coe_natCast coe_intCast

instance {Œ± : Type*} {Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤]
    [NonUnitalCommSemiring Œ≤] [IsTopologicalSemiring Œ≤] : NonUnitalCommSemiring C(Œ±, Œ≤) :=
  coe_injective.nonUnitalCommSemiring _ coe_zero coe_add coe_mul coe_nsmul

instance {Œ± : Type*} {Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [CommSemiring Œ≤]
    [IsTopologicalSemiring Œ≤] : CommSemiring C(Œ±, Œ≤) :=
  coe_injective.commSemiring _ coe_zero coe_one coe_add coe_mul coe_nsmul coe_pow coe_natCast

instance {Œ± : Type*} {Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [NonUnitalCommRing Œ≤]
    [IsTopologicalRing Œ≤] : NonUnitalCommRing C(Œ±, Œ≤) :=
  coe_injective.nonUnitalCommRing _ coe_zero coe_add coe_mul coe_neg coe_sub coe_nsmul coe_zsmul

instance {Œ± : Type*} {Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [CommRing Œ≤]
    [IsTopologicalRing Œ≤] : CommRing C(Œ±, Œ≤) :=
  coe_injective.commRing _ coe_zero coe_one coe_add coe_mul coe_neg coe_sub coe_nsmul coe_zsmul
    coe_pow coe_natCast coe_intCast

instance {Œ± : Type*} {Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [LocallyCompactSpace Œ±]
    [NonUnitalSemiring Œ≤] [IsTopologicalSemiring Œ≤] : IsTopologicalSemiring C(Œ±, Œ≤) where

instance {Œ± : Type*} {Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [LocallyCompactSpace Œ±]
    [NonUnitalRing Œ≤] [IsTopologicalRing Œ≤] : IsTopologicalRing C(Œ±, Œ≤) where

/-- Composition on the left by a (continuous) homomorphism of topological semirings, as a
`RingHom`.  Similar to `RingHom.compLeft`. -/
@[simps!]
protected def _root_.RingHom.compLeftContinuous (Œ± : Type*) {Œ≤ : Type*} {Œ≥ : Type*}
    [TopologicalSpace Œ±]
    [TopologicalSpace Œ≤] [Semiring Œ≤] [IsTopologicalSemiring Œ≤] [TopologicalSpace Œ≥] [Semiring Œ≥]
    [IsTopologicalSemiring Œ≥] (g : Œ≤ ‚Üí+* Œ≥) (hg : Continuous g) : C(Œ±, Œ≤) ‚Üí+* C(Œ±, Œ≥) :=
  { g.toMonoidHom.compLeftContinuous Œ± hg, g.toAddMonoidHom.compLeftContinuous Œ± hg with }

/-- Coercion to a function as a `RingHom`. -/
@[simps!]
def coeFnRingHom {Œ± : Type*} {Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [Semiring Œ≤]
    [IsTopologicalSemiring Œ≤] : C(Œ±, Œ≤) ‚Üí+* Œ± ‚Üí Œ≤ :=
  { (coeFnMonoidHom : C(Œ±, Œ≤) ‚Üí* _),
    (coeFnAddMonoidHom : C(Œ±, Œ≤) ‚Üí+ _) with }

end ContinuousMap

end RingStructure

attribute [local ext] Subtype.eq

section ModuleStructure

/-!
### Module structure

In this section we show that continuous functions valued in a topological module `M` over a
topological semiring `R` inherit the structure of a module.
-/


section Subtype

variable (Œ± : Type*) [TopologicalSpace Œ±]
variable (R : Type*) [Semiring R]
variable (M : Type*) [TopologicalSpace M] [AddCommGroup M]
variable [Module R M] [ContinuousConstSMul R M] [IsTopologicalAddGroup M]

/-- The `R`-submodule of continuous maps `Œ± ‚Üí M`. -/
def continuousSubmodule : Submodule R (Œ± ‚Üí M) :=
  { continuousAddSubgroup Œ± M with
    carrier := { f : Œ± ‚Üí M | Continuous f }
    smul_mem' := fun c _ hf => hf.const_smul c }

end Subtype

namespace ContinuousMap

variable {Œ± Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] {R R‚ÇÅ : Type*} {M : Type*}
  [TopologicalSpace M] {M‚ÇÇ : Type*} [TopologicalSpace M‚ÇÇ]

@[to_additive]
instance instSMul [SMul R M] [ContinuousConstSMul R M] : SMul R C(Œ±, M) :=
  ‚ü®fun r f => ‚ü®r ‚Ä¢ ‚áëf, f.continuous.const_smul r‚ü©‚ü©

@[to_additive]
instance [SMul R M] [ContinuousConstSMul R M] : ContinuousConstSMul R C(Œ±, M) where
  continuous_const_smul r := continuous_postcomp ‚ü®_, continuous_const_smul r‚ü©

@[to_additive]
instance [TopologicalSpace R] [SMul R M] [ContinuousSMul R M] :
    ContinuousSMul R C(Œ±, M) :=
  ‚ü®(continuous_postcomp ‚ü®_, continuous_smul‚ü©).comp continuous_prodMk_const‚ü©

@[to_additive (attr := simp, norm_cast)]
theorem coe_smul [SMul R M] [ContinuousConstSMul R M] (c : R) (f : C(Œ±, M)) : ‚áë(c ‚Ä¢ f) = c ‚Ä¢ ‚áëf :=
  rfl

@[to_additive]
theorem smul_apply [SMul R M] [ContinuousConstSMul R M] (c : R) (f : C(Œ±, M)) (a : Œ±) :
    (c ‚Ä¢ f) a = c ‚Ä¢ f a :=
  rfl

@[to_additive (attr := simp)]
theorem smul_comp [SMul R M] [ContinuousConstSMul R M] (r : R) (f : C(Œ≤, M)) (g : C(Œ±, Œ≤)) :
    (r ‚Ä¢ f).comp g = r ‚Ä¢ f.comp g :=
  rfl

@[to_additive]
instance [SMul R M] [ContinuousConstSMul R M] [SMul R‚ÇÅ M] [ContinuousConstSMul R‚ÇÅ M]
    [SMulCommClass R R‚ÇÅ M] : SMulCommClass R R‚ÇÅ C(Œ±, M) where
  smul_comm _ _ _ := ext fun _ => smul_comm _ _ _

instance [SMul R M] [ContinuousConstSMul R M] [SMul R‚ÇÅ M] [ContinuousConstSMul R‚ÇÅ M] [SMul R R‚ÇÅ]
    [IsScalarTower R R‚ÇÅ M] : IsScalarTower R R‚ÇÅ C(Œ±, M) where
  smul_assoc _ _ _ := ext fun _ => smul_assoc _ _ _

instance [SMul R M] [SMul R·µê·µí·µñ M] [ContinuousConstSMul R M] [IsCentralScalar R M] :
    IsCentralScalar R C(Œ±, M) where op_smul_eq_smul _ _ := ext fun _ => op_smul_eq_smul _ _

instance [SMul R M] [ContinuousConstSMul R M] [Mul M] [ContinuousMul M] [IsScalarTower R M M] :
    IsScalarTower R C(Œ±, M) C(Œ±, M) where
  smul_assoc _ _ _ := ext fun _ => smul_mul_assoc ..

instance [SMul R M] [ContinuousConstSMul R M] [Mul M] [ContinuousMul M] [SMulCommClass R M M] :
    SMulCommClass R C(Œ±, M) C(Œ±, M) where
  smul_comm _ _ _ := ext fun _ => (mul_smul_comm ..).symm

instance [SMul R M] [ContinuousConstSMul R M] [Mul M] [ContinuousMul M] [SMulCommClass M R M] :
    SMulCommClass C(Œ±, M) R C(Œ±, M) where
  smul_comm _ _ _ := ext fun _ => smul_comm (_ : M) ..

instance [Monoid R] [MulAction R M] [ContinuousConstSMul R M] : MulAction R C(Œ±, M) :=
  Function.Injective.mulAction _ coe_injective coe_smul

instance [Monoid R] [AddMonoid M] [DistribMulAction R M] [ContinuousAdd M]
    [ContinuousConstSMul R M] : DistribMulAction R C(Œ±, M) :=
  Function.Injective.distribMulAction coeFnAddMonoidHom coe_injective coe_smul

variable [Semiring R] [AddCommMonoid M] [AddCommMonoid M‚ÇÇ]
variable [ContinuousAdd M] [Module R M] [ContinuousConstSMul R M]
variable [ContinuousAdd M‚ÇÇ] [Module R M‚ÇÇ] [ContinuousConstSMul R M‚ÇÇ]

instance module : Module R C(Œ±, M) :=
  Function.Injective.module R coeFnAddMonoidHom coe_injective coe_smul

variable (R)

/-- Composition on the left by a continuous linear map, as a `ContinuousLinearMap`.
Similar to `LinearMap.compLeft`. -/
@[simps]
protected def _root_.ContinuousLinearMap.compLeftContinuous (Œ± : Type*) [TopologicalSpace Œ±]
    (g : M ‚ÜíL[R] M‚ÇÇ) : C(Œ±, M) ‚ÜíL[R] C(Œ±, M‚ÇÇ) where
  __ := g.toLinearMap.toAddMonoidHom.compLeftContinuous Œ± g.continuous
  map_smul' := fun c _ => ext fun _ => g.map_smul' c _
  cont := ContinuousMap.continuous_postcomp _

/-- The constant map `x ‚Ü¶ y ‚Ü¶ x` as a `ContinuousLinearMap`. -/
@[simps!]
def _root_.ContinuousLinearMap.const (Œ± : Type*) [TopologicalSpace Œ±] : M ‚ÜíL[R] C(Œ±, M) where
  toFun m := .const Œ± m
  map_add' _ _ := rfl
  map_smul' _ _ := rfl
  cont := ContinuousMap.continuous_const'

/-- Coercion to a function as a `LinearMap`. -/
@[simps]
def coeFnLinearMap : C(Œ±, M) ‚Üí‚Çó[R] Œ± ‚Üí M :=
  { (coeFnAddMonoidHom : C(Œ±, M) ‚Üí+ _) with
    map_smul' := coe_smul }

/-- Evaluation at a point, as a continuous linear map. -/
@[simps apply]
def evalCLM (x : Œ±) : C(Œ±, M) ‚ÜíL[R] M where
  toFun f := f x
  map_add' _ _ := add_apply _ _ x
  map_smul' _ _ := smul_apply _ _ x

end ContinuousMap

end ModuleStructure

section AlgebraStructure

/-!
### Algebra structure

In this section we show that continuous functions valued in a topological algebra `A` over a ring
`R` inherit the structure of an algebra. Note that the hypothesis that `A` is a topological algebra
is obtained by requiring that `A` be both a `ContinuousSMul` and a `IsTopologicalSemiring`. -/


section Subtype

variable {Œ± : Type*} [TopologicalSpace Œ±] {R : Type*} [CommSemiring R] {A : Type*}
  [TopologicalSpace A] [Semiring A] [Algebra R A] [IsTopologicalSemiring A]

/-- The `R`-subalgebra of continuous maps `Œ± ‚Üí A`. -/
def continuousSubalgebra : Subalgebra R (Œ± ‚Üí A) :=
  { continuousSubsemiring Œ± A with
    carrier := { f : Œ± ‚Üí A | Continuous f }
    algebraMap_mem' := fun r => (continuous_const : Continuous fun _ : Œ± => algebraMap R A r) }

end Subtype

section ContinuousMap

variable {Œ± : Type*} [TopologicalSpace Œ±] {R : Type*} [CommSemiring R] {A : Type*}
  [TopologicalSpace A] [Semiring A] [Algebra R A] [IsTopologicalSemiring A] {A‚ÇÇ : Type*}
  [TopologicalSpace A‚ÇÇ] [Semiring A‚ÇÇ] [Algebra R A‚ÇÇ] [IsTopologicalSemiring A‚ÇÇ]

/-- Continuous constant functions as a `RingHom`. -/
def ContinuousMap.C : R ‚Üí+* C(Œ±, A) where
  toFun := fun c : R => ‚ü®fun _ : Œ± => (algebraMap R A) c, continuous_const‚ü©
  map_one' := by ext _; exact (algebraMap R A).map_one
  map_mul' c‚ÇÅ c‚ÇÇ := by ext _; exact (algebraMap R A).map_mul _ _
  map_zero' := by ext _; exact (algebraMap R A).map_zero
  map_add' c‚ÇÅ c‚ÇÇ := by ext _; exact (algebraMap R A).map_add _ _

@[simp]
theorem ContinuousMap.C_apply (r : R) (a : Œ±) : ContinuousMap.C r a = algebraMap R A r :=
  rfl

instance ContinuousMap.algebra : Algebra R C(Œ±, A) where
  algebraMap := ContinuousMap.C
  commutes' c f := by ext x; exact Algebra.commutes' _ _
  smul_def' c f := by ext x; exact Algebra.smul_def' _ _

variable (R)

/-- Composition on the left by a (continuous) homomorphism of topological `R`-algebras, as an
`AlgHom`. Similar to `AlgHom.compLeft`. -/
@[simps!]
protected def AlgHom.compLeftContinuous {Œ± : Type*} [TopologicalSpace Œ±] (g : A ‚Üí‚Çê[R] A‚ÇÇ)
    (hg : Continuous g) : C(Œ±, A) ‚Üí‚Çê[R] C(Œ±, A‚ÇÇ) :=
  { g.toRingHom.compLeftContinuous Œ± hg with
    commutes' := fun _ => ContinuousMap.ext fun _ => g.commutes' _ }

variable (A)

/-- Precomposition of functions into a topological semiring by a continuous map is an algebra
homomorphism. -/
@[simps]
def ContinuousMap.compRightAlgHom {Œ± Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤]
    (f : C(Œ±, Œ≤)) : C(Œ≤, A) ‚Üí‚Çê[R] C(Œ±, A) where
  toFun g := g.comp f
  map_zero' := ext fun _ ‚Ü¶ rfl
  map_add'  _ _ := ext fun _ ‚Ü¶ rfl
  map_one' := ext fun _ ‚Ü¶ rfl
  map_mul' _ _ := ext fun _ ‚Ü¶ rfl
  commutes' _ := ext fun _ ‚Ü¶ rfl

theorem ContinuousMap.compRightAlgHom_continuous {Œ± Œ≤ : Type*} [TopologicalSpace Œ±]
    [TopologicalSpace Œ≤] (f : C(Œ±, Œ≤)) : Continuous (compRightAlgHom R A f) :=
  continuous_precomp f

variable {A}

/-- Coercion to a function as an `AlgHom`. -/
@[simps!]
def ContinuousMap.coeFnAlgHom : C(Œ±, A) ‚Üí‚Çê[R] Œ± ‚Üí A :=
  { (ContinuousMap.coeFnRingHom : C(Œ±, A) ‚Üí+* _) with
    commutes' := fun _ => rfl }

variable {R}

/-- A version of `Set.SeparatesPoints` for subalgebras of the continuous functions,
used for stating the Stone-Weierstrass theorem.
-/
abbrev Subalgebra.SeparatesPoints (s : Subalgebra R C(Œ±, A)) : Prop :=
  Set.SeparatesPoints ((fun f : C(Œ±, A) => (f : Œ± ‚Üí A)) '' (s : Set C(Œ±, A)))

theorem Subalgebra.separatesPoints_monotone :
    Monotone fun s : Subalgebra R C(Œ±, A) => s.SeparatesPoints := fun s s' r h x y n => by
  obtain ‚ü®f, m, w‚ü© := h n
  rcases m with ‚ü®f, ‚ü®m, rfl‚ü©‚ü©
  exact ‚ü®_, ‚ü®f, ‚ü®r m, rfl‚ü©‚ü©, w‚ü©

@[simp]
theorem algebraMap_apply (k : R) (a : Œ±) : algebraMap R C(Œ±, A) k a = k ‚Ä¢ (1 : A) := by
  rw [Algebra.algebraMap_eq_smul_one]
  rfl

variable {ùïú : Type*} [TopologicalSpace ùïú]
variable (s : Set C(Œ±, ùïú)) (f : s) (x : Œ±)

/-- A set of continuous maps "separates points strongly"
if for each pair of distinct points there is a function with specified values on them.

We give a slightly unusual formulation, where the specified values are given by some
function `v`, and we ask `f x = v x ‚àß f y = v y`. This avoids needing a hypothesis `x ‚â† y`.

In fact, this definition would work perfectly well for a set of non-continuous functions,
but as the only current use case is in the Stone-Weierstrass theorem,
writing it this way avoids having to deal with casts inside the set.
(This may need to change if we do Stone-Weierstrass on non-compact spaces,
where the functions would be continuous functions vanishing at infinity.)
-/
def Set.SeparatesPointsStrongly (s : Set C(Œ±, ùïú)) : Prop :=
  ‚àÄ (v : Œ± ‚Üí ùïú) (x y : Œ±), ‚àÉ f ‚àà s, (f x : ùïú) = v x ‚àß f y = v y

variable [Field ùïú] [IsTopologicalRing ùïú]

/-- Working in continuous functions into a topological field,
a subalgebra of functions that separates points also separates points strongly.

By the hypothesis, we can find a function `f` so `f x ‚â† f y`.
By an affine transformation in the field we can arrange so that `f x = a` and `f x = b`.
-/
theorem Subalgebra.SeparatesPoints.strongly {s : Subalgebra ùïú C(Œ±, ùïú)} (h : s.SeparatesPoints) :
    (s : Set C(Œ±, ùïú)).SeparatesPointsStrongly := fun v x y => by
  by_cases n : x = y
  ¬∑ subst n
    exact ‚ü®_, (v x ‚Ä¢ (1 : s) : s).prop, mul_one _, mul_one _‚ü©
  obtain ‚ü®_, ‚ü®f, hf, rfl‚ü©, hxy‚ü© := h n
  replace hxy : f x - f y ‚â† 0 := sub_ne_zero_of_ne hxy
  let a := v x
  let b := v y
  let f' : s :=
    ((b - a) * (f x - f y)‚Åª¬π) ‚Ä¢ (algebraMap _ s (f x) - (‚ü®f, hf‚ü© : s)) + algebraMap _ s a
  refine ‚ü®f', f'.prop, ?_, ?_‚ü©
  ¬∑ simp [a, b, f']
  ¬∑ simp [a, b, f', inv_mul_cancel_right‚ÇÄ hxy]

end ContinuousMap

instance ContinuousMap.subsingleton_subalgebra (Œ± : Type*) [TopologicalSpace Œ±] (R : Type*)
    [CommSemiring R] [TopologicalSpace R] [IsTopologicalSemiring R] [Subsingleton Œ±] :
    Subsingleton (Subalgebra R C(Œ±, R)) :=
  ‚ü®fun s‚ÇÅ s‚ÇÇ => by
    cases isEmpty_or_nonempty Œ±
    ¬∑ have : Subsingleton C(Œ±, R) := DFunLike.coe_injective.subsingleton
      subsingleton
    ¬∑ inhabit Œ±
      ext f
      have h : f = algebraMap R C(Œ±, R) (f default) := by
        ext x'
        simp only [mul_one, Algebra.id.smul_eq_mul, algebraMap_apply]
        congr
        simp [eq_iff_true_of_subsingleton]
      rw [h]
      simp only [Subalgebra.algebraMap_mem]‚ü©

end AlgebraStructure

section ModuleOverContinuousFunctions

/-!
### Structure as module over scalar functions

If `M` is a module over `R`, then we show that the space of continuous functions from `Œ±` to `M`
is naturally a module over the ring of continuous functions from `Œ±` to `R`. -/

namespace ContinuousMap

variable
  {Œ± : Type*} [TopologicalSpace Œ±]
  {R : Type*} [Semiring R] [TopologicalSpace R]
  {M : Type*} [TopologicalSpace M] [AddCommMonoid M] [Module R M] [ContinuousSMul R M]

instance instSMul' : SMul C(Œ±, R) C(Œ±, M) :=
  ‚ü®fun f g => ‚ü®fun x => f x ‚Ä¢ g x, Continuous.smul f.2 g.2‚ü©‚ü©

/-- Coercion to a function for a scalar-valued continuous map multiplying a vector-valued one
(as opposed to `ContinuousMap.coe_smul` which is multiplication by a constant scalar). -/
@[simp] lemma coe_smul' (f : C(Œ±, R)) (g : C(Œ±, M)) :
    ‚áë(f ‚Ä¢ g) = ‚áëf ‚Ä¢ ‚áëg :=
  rfl

/-- Evaluation of a scalar-valued continuous map multiplying a vector-valued one
(as opposed to `ContinuousMap.smul_apply` which is multiplication by a constant scalar). -/
-- (this doesn't need to be @[simp] since it can be derived from `coe_smul'` and `Pi.smul_apply'`)
lemma smul_apply' (f : C(Œ±, R)) (g : C(Œ±, M)) (x : Œ±) :
    (f ‚Ä¢ g) x = f x ‚Ä¢ g x :=
  rfl

instance module' [IsTopologicalSemiring R] [ContinuousAdd M] :
    Module C(Œ±, R) C(Œ±, M) where
  smul := (¬∑ ‚Ä¢ ¬∑)
  smul_add c f g := by ext x; exact smul_add (c x) (f x) (g x)
  add_smul c‚ÇÅ c‚ÇÇ f := by ext x; exact add_smul (c‚ÇÅ x) (c‚ÇÇ x) (f x)
  mul_smul c‚ÇÅ c‚ÇÇ f := by ext x; exact mul_smul (c‚ÇÅ x) (c‚ÇÇ x) (f x)
  one_smul f := by ext x; exact one_smul R (f x)
  zero_smul f := by ext x; exact zero_smul _ _
  smul_zero r := by ext x; exact smul_zero _

end ContinuousMap

end ModuleOverContinuousFunctions

/-! ### Evaluation as a bundled map -/

variable {X : Type*} (S R : Type*) [TopologicalSpace X] [CommSemiring S] [CommSemiring R]
variable [Algebra S R] [TopologicalSpace R] [IsTopologicalSemiring R]

/-- Evaluation of continuous maps at a point, bundled as an algebra homomorphism. -/
@[simps]
def ContinuousMap.evalAlgHom (x : X) : C(X, R) ‚Üí‚Çê[S] R where
  toFun f := f x
  map_zero' := rfl
  map_one' := rfl
  map_add' _ _ := rfl
  map_mul' _ _ := rfl
  commutes' _ := rfl
