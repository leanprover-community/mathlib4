/-
Copyright (c) 2024 Yoh Tanimoto. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yoh Tanimoto
-/
import Mathlib.Topology.Algebra.Support
import Mathlib.Topology.ContinuousMap.CocompactMap
import Mathlib.Topology.ContinuousMap.Ordered
import Mathlib.Topology.ContinuousMap.ZeroAtInfty

/-!
# Compactly supported continuous functions

In this file, we define the type `C_c(Œ±, Œ≤)` of compactly supported continuous functions and the
class `CompactlySupportedContinuousMapClass`, and prove basic properties.

## Main definitions and results

This file contains various instances such as `Add`, `Mul`, `SMul F C_c(Œ±, Œ≤)` when `F` is a class of
continuous functions.
When `Œ≤` has more structures, `C_c(Œ±, Œ≤)` inherits such structures as `AddCommGroup`,
`NonUnitalRing` and `StarRing`.

When the domain `Œ±` is compact, `ContinuousMap.liftCompactlySupported` gives the identification
`C(Œ±, Œ≤) ‚âÉ C_c(Œ±, Œ≤)`.

-/

variable {F Œ± Œ≤ Œ≥ : Type*} [TopologicalSpace Œ±]

/-- `C_c(Œ±, Œ≤)` is the type of continuous functions `Œ± ‚Üí Œ≤` with compact support from a topological
space to a topological space with a zero element.

When possible, instead of parametrizing results over `f : C_c(Œ±, Œ≤)`,
you should parametrize over `{F : Type*} [CompactlySupportedContinuousMapClass F Œ± Œ≤] (f : F)`.

When you extend this structure, make sure to extend `CompactlySupportedContinuousMapClass`. -/
structure CompactlySupportedContinuousMap (Œ± Œ≤ : Type*) [TopologicalSpace Œ±] [Zero Œ≤]
    [TopologicalSpace Œ≤] extends ContinuousMap Œ± Œ≤ where
  /-- The function has compact support . -/
  hasCompactSupport' : HasCompactSupport toFun

@[inherit_doc]
scoped[CompactlySupported] notation (priority := 2000)
  "C_c(" Œ± ", " Œ≤ ")" => CompactlySupportedContinuousMap Œ± Œ≤

@[inherit_doc]
scoped[CompactlySupported] notation Œ± " ‚ÜíC_c " Œ≤ => CompactlySupportedContinuousMap Œ± Œ≤

open CompactlySupported

section

/-- `CompactlySupportedContinuousMapClass F Œ± Œ≤` states that `F` is a type of continuous maps with
compact support.

You should also extend this typeclass when you extend `CompactlySupportedContinuousMap`. -/
class CompactlySupportedContinuousMapClass (F : Type*) (Œ± Œ≤ : outParam <| Type*)
    [TopologicalSpace Œ±] [Zero Œ≤] [TopologicalSpace Œ≤] [FunLike F Œ± Œ≤]
    extends ContinuousMapClass F Œ± Œ≤ : Prop where
  /-- Each member of the class has compact support. -/
  hasCompactSupport (f : F) : HasCompactSupport f

end

namespace CompactlySupportedContinuousMap

section Basics

variable [TopologicalSpace Œ≤] [Zero Œ≤]

instance : FunLike C_c(Œ±, Œ≤) Œ± Œ≤ where
  coe f := f.toFun
  coe_injective' f g h := by
    obtain ‚ü®‚ü®_, _‚ü©, _‚ü© := f
    obtain ‚ü®‚ü®_, _‚ü©, _‚ü© := g
    congr

protected lemma hasCompactSupport (f : C_c(Œ±, Œ≤)) : HasCompactSupport f := f.hasCompactSupport'

instance : CompactlySupportedContinuousMapClass C_c(Œ±, Œ≤) Œ± Œ≤ where
  map_continuous f := f.continuous_toFun
  hasCompactSupport f := f.hasCompactSupport'

@[simp]
theorem coe_toContinuousMap (f : C_c(Œ±, Œ≤)) : (f.toContinuousMap : Œ± ‚Üí Œ≤) = f :=
  rfl

@[ext]
theorem ext {f g : C_c(Œ±, Œ≤)} (h : ‚àÄ x, f x = g x) : f = g :=
  DFunLike.ext _ _ h

@[simp]
theorem coe_mk (f : C(Œ±, Œ≤)) (h : HasCompactSupport f) : ‚áë(‚ü®f, h‚ü© : C_c(Œ±, Œ≤)) = f :=
  rfl

/-- Copy of a `CompactlySupportedContinuousMap` with a new `toFun` equal to the old one. Useful
to fix definitional equalities. -/
protected def copy (f : C_c(Œ±, Œ≤)) (f' : Œ± ‚Üí Œ≤) (h : f' = f) : C_c(Œ±, Œ≤) where
  toFun := f'
  continuous_toFun := by
    rw [h]
    exact f.continuous_toFun
  hasCompactSupport' := by
    simp_rw [h]
    exact f.hasCompactSupport'

@[simp]
theorem coe_copy (f : C_c(Œ±, Œ≤)) (f' : Œ± ‚Üí Œ≤) (h : f' = f) : ‚áë(f.copy f' h) = f' :=
  rfl

theorem copy_eq (f : C_c(Œ±, Œ≤)) (f' : Œ± ‚Üí Œ≤) (h : f' = f) : f.copy f' h = f :=
  DFunLike.ext' h

theorem eq_of_empty [IsEmpty Œ±] (f g : C_c(Œ±, Œ≤)) : f = g :=
  ext <| IsEmpty.elim ‚Äπ_‚Ä∫

/-- A continuous function on a compact space automatically has compact support. -/
@[simps]
def ContinuousMap.liftCompactlySupported [CompactSpace Œ±] : C(Œ±, Œ≤) ‚âÉ C_c(Œ±, Œ≤) where
  toFun f :=
    { toFun := f
      hasCompactSupport' := HasCompactSupport.of_compactSpace f }
  invFun f := f
  left_inv _ := rfl
  right_inv _ := rfl

end Basics

/-! ### Algebraic structure

Whenever `Œ≤` has the structure of continuous additive monoid and a compatible topological structure,
then `C_c(Œ±, Œ≤)` inherits a corresponding algebraic structure. The primary exception to this is that
`C_c(Œ±, Œ≤)` will not have a multiplicative identity.
-/

section AlgebraicStructure

variable [TopologicalSpace Œ≤] (x : Œ±)

instance [Zero Œ≤] : Zero C_c(Œ±, Œ≤) where
  zero := { toFun := (0 : C(Œ±, Œ≤))
            continuous_toFun := (0 : C(Œ±, Œ≤)).2
            hasCompactSupport' := by simp [HasCompactSupport, tsupport] }

instance [Zero Œ≤] : Inhabited C_c(Œ±, Œ≤) :=
  ‚ü®0‚ü©

@[simp]
theorem coe_zero [Zero Œ≤] : ‚áë(0 : C_c(Œ±, Œ≤)) = 0 :=
  rfl

theorem zero_apply [Zero Œ≤] : (0 : C_c(Œ±, Œ≤)) x = 0 :=
  rfl

instance [MulZeroClass Œ≤] [ContinuousMul Œ≤] : Mul C_c(Œ±, Œ≤) :=
  ‚ü®fun f g => ‚ü®f * g, HasCompactSupport.mul_left g.2‚ü©‚ü©

@[simp]
theorem coe_mul [MulZeroClass Œ≤] [ContinuousMul Œ≤] (f g : C_c(Œ±, Œ≤)) : ‚áë(f * g) = f * g :=
  rfl

theorem mul_apply [MulZeroClass Œ≤] [ContinuousMul Œ≤] (f g : C_c(Œ±, Œ≤)) : (f * g) x = f x * g x :=
  rfl

/-- the product of `f : F` assuming `ContinuousMapClass F Œ± Œ≥` and `ContinuousSMul Œ≥ Œ≤` and
`g : C_c(Œ±, Œ≤)` is in `C_c(Œ±, Œ≤)` -/
instance [Zero Œ≤] [TopologicalSpace Œ≥] [SMulZeroClass Œ≥ Œ≤] [ContinuousSMul Œ≥ Œ≤]
    {F : Type*} [FunLike F Œ± Œ≥] [ContinuousMapClass F Œ± Œ≥] : SMul F C_c(Œ±, Œ≤) where
  smul f g :=
    ‚ü®‚ü®fun x ‚Ü¶ f x ‚Ä¢ g x, (map_continuous f).smul (map_continuous g)‚ü©, g.hasCompactSupport.smul_left‚ü©

@[simp]
theorem coe_smulc [Zero Œ≤] [TopologicalSpace Œ≥] [SMulZeroClass Œ≥ Œ≤] [ContinuousSMul Œ≥ Œ≤]
    {F : Type*} [FunLike F Œ± Œ≥] [ContinuousMapClass F Œ± Œ≥] (f : F) (g : C_c(Œ±, Œ≤)) :
    ‚áë(f ‚Ä¢ g) = fun x => f x ‚Ä¢ g x :=
  rfl

theorem smulc_apply [Zero Œ≤] [TopologicalSpace Œ≥] [SMulZeroClass Œ≥ Œ≤] [ContinuousSMul Œ≥ Œ≤]
    {F : Type*} [FunLike F Œ± Œ≥] [ContinuousMapClass F Œ± Œ≥] (f : F) (g : C_c(Œ±, Œ≤)) (x : Œ±) :
    (f ‚Ä¢ g) x = f x ‚Ä¢ g x :=
  rfl

instance [MulZeroClass Œ≤] [ContinuousMul Œ≤] : MulZeroClass C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.mulZeroClass _ coe_zero coe_mul

instance [SemigroupWithZero Œ≤] [ContinuousMul Œ≤] :
    SemigroupWithZero C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.semigroupWithZero _ coe_zero coe_mul

instance [AddZeroClass Œ≤] [ContinuousAdd Œ≤] : Add C_c(Œ±, Œ≤) :=
  ‚ü®fun f g => ‚ü®f + g, HasCompactSupport.add f.2 g.2‚ü©‚ü©

@[simp]
theorem coe_add [AddZeroClass Œ≤] [ContinuousAdd Œ≤] (f g : C_c(Œ±, Œ≤)) : ‚áë(f + g) = f + g :=
  rfl

theorem add_apply [AddZeroClass Œ≤] [ContinuousAdd Œ≤] (f g : C_c(Œ±, Œ≤)) : (f + g) x = f x + g x :=
  rfl

instance [AddZeroClass Œ≤] [ContinuousAdd Œ≤] : AddZeroClass C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.addZeroClass _ coe_zero coe_add

/-- Coercion to a function as a `AddMonoidHom`. Similar to `AddMonoidHom.coeFn`. -/
def coeFnMonoidHom [AddMonoid Œ≤] [ContinuousAdd Œ≤] : C_c(Œ±, Œ≤) ‚Üí+ Œ± ‚Üí Œ≤ where
  toFun f := f
  map_zero' := coe_zero
  map_add' := coe_add

instance [Zero Œ≤] {R : Type*} [SMulZeroClass R Œ≤] [ContinuousConstSMul R Œ≤] :
    SMul R C_c(Œ±, Œ≤) :=
  ‚ü®fun r f => ‚ü®‚ü®r ‚Ä¢ ‚áëf, (map_continuous f).const_smul r‚ü©, HasCompactSupport.smul_left f.2‚ü©‚ü©

@[simp, norm_cast]
theorem coe_smul [Zero Œ≤] {R : Type*} [SMulZeroClass R Œ≤] [ContinuousConstSMul R Œ≤] (r : R)
    (f : C_c(Œ±, Œ≤)) : ‚áë(r ‚Ä¢ f) = r ‚Ä¢ ‚áëf :=
  rfl

theorem smul_apply [Zero Œ≤] {R : Type*} [SMulZeroClass R Œ≤] [ContinuousConstSMul R Œ≤] (r : R)
    (f : C_c(Œ±, Œ≤)) (x : Œ±) : (r ‚Ä¢ f) x = r ‚Ä¢ f x :=
  rfl

section AddMonoid

instance [AddMonoid Œ≤] [ContinuousAdd Œ≤] : AddMonoid C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.addMonoid _ coe_zero coe_add fun _ _ => rfl

end AddMonoid

instance [AddCommMonoid Œ≤] [ContinuousAdd Œ≤] : AddCommMonoid C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.addCommMonoid _ coe_zero coe_add fun _ _ => rfl

@[simp]
theorem coe_sum [AddCommMonoid Œ≤] [ContinuousAdd Œ≤] {Œπ : Type*} (s : Finset Œπ) (f : Œπ ‚Üí C_c(Œ±, Œ≤)) :
    ‚áë(‚àë i in s, f i) = ‚àë i in s, (f i : Œ± ‚Üí Œ≤) :=
  map_sum coeFnMonoidHom f s

theorem sum_apply [AddCommMonoid Œ≤] [ContinuousAdd Œ≤] {Œπ : Type*} (s : Finset Œπ) (f : Œπ ‚Üí C_c(Œ±, Œ≤))
    (a : Œ±) : (‚àë i in s, f i) a = ‚àë i in s, f i a := by simp

section AddGroup

variable [AddGroup Œ≤] [TopologicalAddGroup Œ≤] (f g : C_c(Œ±, Œ≤))

instance : Neg C_c(Œ±, Œ≤) where
  neg f := { toFun := -f.1
             continuous_toFun := map_continuous (-f.1)
             hasCompactSupport' := by simpa [HasCompactSupport, tsupport] using f.2 }

@[simp]
theorem coe_neg : ‚áë(-f) = -f :=
  rfl

theorem neg_apply : (-f) x = -f x :=
  rfl

instance : Sub C_c(Œ±, Œ≤) where
  sub f g := { toFun := f.1 - g.1
               continuous_toFun := map_continuous (f.1 - g.1)
               hasCompactSupport' := by
                 simpa [sub_eq_add_neg] using HasCompactSupport.add f.2 (-g).2 }

@[simp]
theorem coe_sub : ‚áë(f - g) = f - g :=
  rfl

theorem sub_apply : (f - g) x = f x - g x :=
  rfl

instance : AddGroup C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.addGroup _ coe_zero coe_add coe_neg coe_sub (fun _ _ => rfl) fun _ _ => rfl

end AddGroup

instance [AddCommGroup Œ≤] [TopologicalAddGroup Œ≤] : AddCommGroup C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.addCommGroup _ coe_zero coe_add coe_neg coe_sub (fun _ _ => rfl) fun _ _ =>
    rfl

instance [Zero Œ≤] {R : Type*} [Zero R] [SMulWithZero R Œ≤] [SMulWithZero R·µê·µí·µñ Œ≤]
    [ContinuousConstSMul R Œ≤] [IsCentralScalar R Œ≤] : IsCentralScalar R C_c(Œ±, Œ≤) :=
  ‚ü®fun _ _ => ext fun _ => op_smul_eq_smul _ _‚ü©

instance [Zero Œ≤] {R : Type*} [Zero R] [SMulWithZero R Œ≤]
    [ContinuousConstSMul R Œ≤] : SMulWithZero R C_c(Œ±, Œ≤) :=
  Function.Injective.smulWithZero ‚ü®_, coe_zero‚ü© DFunLike.coe_injective coe_smul

instance [Zero Œ≤] {R : Type*} [MonoidWithZero R] [MulActionWithZero R Œ≤]
    [ContinuousConstSMul R Œ≤] : MulActionWithZero R C_c(Œ±, Œ≤) :=
  Function.Injective.mulActionWithZero ‚ü®_, coe_zero‚ü© DFunLike.coe_injective coe_smul

instance [AddCommMonoid Œ≤] [ContinuousAdd Œ≤] {R : Type*} [Semiring R] [Module R Œ≤]
    [ContinuousConstSMul R Œ≤] : Module R C_c(Œ±, Œ≤) :=
  Function.Injective.module R ‚ü®‚ü®_, coe_zero‚ü©, coe_add‚ü© DFunLike.coe_injective coe_smul

instance [NonUnitalNonAssocSemiring Œ≤] [TopologicalSemiring Œ≤] :
    NonUnitalNonAssocSemiring C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.nonUnitalNonAssocSemiring _ coe_zero coe_add coe_mul fun _ _ => rfl

instance [NonUnitalSemiring Œ≤] [TopologicalSemiring Œ≤] :
    NonUnitalSemiring C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.nonUnitalSemiring _ coe_zero coe_add coe_mul fun _ _ => rfl

instance [NonUnitalCommSemiring Œ≤] [TopologicalSemiring Œ≤] :
    NonUnitalCommSemiring C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.nonUnitalCommSemiring _ coe_zero coe_add coe_mul fun _ _ => rfl

instance [NonUnitalNonAssocRing Œ≤] [TopologicalRing Œ≤] :
    NonUnitalNonAssocRing C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.nonUnitalNonAssocRing _ coe_zero coe_add coe_mul coe_neg coe_sub
    (fun _ _ => rfl) fun _ _ => rfl

instance [NonUnitalRing Œ≤] [TopologicalRing Œ≤] : NonUnitalRing C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.nonUnitalRing _ coe_zero coe_add coe_mul coe_neg coe_sub (fun _ _ => rfl)
    fun _ _ => rfl

instance [NonUnitalCommRing Œ≤] [TopologicalRing Œ≤] :
    NonUnitalCommRing C_c(Œ±, Œ≤) :=
  DFunLike.coe_injective.nonUnitalCommRing _ coe_zero coe_add coe_mul coe_neg coe_sub
    (fun _ _ => rfl) fun _ _ => rfl

instance {R : Type*} [Semiring R] [NonUnitalNonAssocSemiring Œ≤]
    [TopologicalSemiring Œ≤] [Module R Œ≤] [ContinuousConstSMul R Œ≤] [IsScalarTower R Œ≤ Œ≤] :
    IsScalarTower R C_c(Œ±, Œ≤) C_c(Œ±, Œ≤) where
  smul_assoc r f g := by
    ext
    simp only [smul_eq_mul, coe_mul, coe_smul, Pi.mul_apply, Pi.smul_apply]
    rw [‚Üê smul_eq_mul, ‚Üê smul_eq_mul, smul_assoc]

instance {R : Type*} [Semiring R] [NonUnitalNonAssocSemiring Œ≤]
    [TopologicalSemiring Œ≤] [Module R Œ≤] [ContinuousConstSMul R Œ≤] [SMulCommClass R Œ≤ Œ≤] :
    SMulCommClass R C_c(Œ±, Œ≤) C_c(Œ±, Œ≤) where
  smul_comm r f g := by
    ext
    simp only [smul_eq_mul, coe_smul, coe_mul, Pi.smul_apply, Pi.mul_apply]
    rw [‚Üê smul_eq_mul, ‚Üê smul_eq_mul, smul_comm]

end AlgebraicStructure

section Star

/-! ### Star structure

It is possible to equip `C_c(Œ±, Œ≤)` with a pointwise `star` operation whenever there is a continuous
`star : Œ≤ ‚Üí Œ≤` for which `star (0 : Œ≤) = 0`. We don't have quite this weak a typeclass, but
`StarAddMonoid` is close enough.

The `StarAddMonoid` class on `C_c(Œ±, Œ≤)` is inherited from their counterparts on `Œ± ‚Üí·µá Œ≤`.
-/


variable [TopologicalSpace Œ≤] [AddMonoid Œ≤] [StarAddMonoid Œ≤] [ContinuousStar Œ≤]

instance : Star C_c(Œ±, Œ≤) where
  star f :=
    { toFun := fun x => star (f x)
      continuous_toFun := (map_continuous f).star
      hasCompactSupport' := by
        rw [HasCompactSupport, tsupport]
        simp only
        have support_star : (Function.support fun (x : Œ±) => star (f x)) = Function.support f := by
          ext x
          simp only [Function.mem_support, ne_eq, star_eq_zero]
        rw [support_star]
        exact f.2 }

@[simp]
theorem coe_star (f : C_c(Œ±, Œ≤)) : ‚áë(star f) = star (‚áëf) :=
  rfl

theorem star_apply (f : C_c(Œ±, Œ≤)) (x : Œ±) : (star f) x = star (f x) :=
  rfl

instance [TrivialStar Œ≤] : TrivialStar C_c(Œ±, Œ≤) where
    star_trivial f := ext fun x => star_trivial (f x)

instance [ContinuousAdd Œ≤] : StarAddMonoid C_c(Œ±, Œ≤) where
  star_involutive f := ext fun x => star_star (f x)
  star_add f g := ext fun x => star_add (f x) (g x)

end Star

section StarModule

variable {ùïú : Type*} [Zero ùïú] [Star ùïú] [AddMonoid Œ≤] [StarAddMonoid Œ≤] [TopologicalSpace Œ≤]
  [ContinuousStar Œ≤] [SMulWithZero ùïú Œ≤] [ContinuousConstSMul ùïú Œ≤] [StarModule ùïú Œ≤]

instance : StarModule ùïú C_c(Œ±, Œ≤) where
  star_smul k f := ext fun x => star_smul k (f x)

end StarModule

section StarRing

variable [NonUnitalSemiring Œ≤] [StarRing Œ≤] [TopologicalSpace Œ≤] [ContinuousStar Œ≤]
  [TopologicalSemiring Œ≤]

instance : StarRing C_c(Œ±, Œ≤) :=
  { CompactlySupportedContinuousMap.instStarAddMonoid with
    star_mul := fun f g => ext fun x => star_mul (f x) (g x) }

end StarRing

/-! ### `C_c` as a functor

For each `Œ≤` with sufficient structure, there is a contravariant functor `C_c(-, Œ≤)` from the
category of topological spaces with morphisms given by `CocompactMap`s.
-/


variable {Œ¥ : Type*} [TopologicalSpace Œ≤] [TopologicalSpace Œ≥] [TopologicalSpace Œ¥]

local notation Œ± " ‚Üíco " Œ≤ => CocompactMap Œ± Œ≤

section

variable [Zero Œ¥]

/-- Composition of a continuous function with compact support with a cocompact map
yields another continuous function with compact support. -/
def comp (f : C_c(Œ≥, Œ¥)) (g : Œ≤ ‚Üíco Œ≥) : C_c(Œ≤, Œ¥) where
  toContinuousMap := (f : C(Œ≥, Œ¥)).comp g
  hasCompactSupport' := by
    apply IsCompact.of_isClosed_subset (g.isCompact_preimage_of_isClosed f.2 (isClosed_tsupport _))
      (isClosed_tsupport (f ‚àò g))
    intro x hx
    rw [tsupport, Set.mem_preimage, _root_.mem_closure_iff]
    intro o ho hgxo
    rw [tsupport, _root_.mem_closure_iff] at hx
    obtain ‚ü®y, hy‚ü© := hx (g ‚Åª¬π' o) (IsOpen.preimage g.1.2 ho) hgxo
    exact ‚ü®g y, hy‚ü©

@[simp]
theorem coe_comp_to_continuous_fun (f : C_c(Œ≥, Œ¥)) (g : Œ≤ ‚Üíco Œ≥) : ((f.comp g) : Œ≤ ‚Üí Œ¥) = f ‚àò g :=
  rfl

@[simp]
theorem comp_id (f : C_c(Œ≥, Œ¥)) : f.comp (CocompactMap.id Œ≥) = f :=
  ext fun _ => rfl

@[simp]
theorem comp_assoc (f : C_c(Œ≥, Œ¥)) (g : Œ≤ ‚Üíco Œ≥) (h : Œ± ‚Üíco Œ≤) :
    (f.comp g).comp h = f.comp (g.comp h) :=
  rfl

@[simp]
theorem zero_comp (g : Œ≤ ‚Üíco Œ≥) : (0 : C_c(Œ≥, Œ¥)).comp g = 0 :=
  rfl

end

/-- Composition as an additive monoid homomorphism. -/
def compAddMonoidHom [AddMonoid Œ¥] [ContinuousAdd Œ¥] (g : Œ≤ ‚Üíco Œ≥) : C_c(Œ≥, Œ¥) ‚Üí+ C_c(Œ≤, Œ¥) where
  toFun f := f.comp g
  map_zero' := zero_comp g
  map_add' _ _ := rfl

/-- Composition as a semigroup homomorphism. -/
def compMulHom [MulZeroClass Œ¥] [ContinuousMul Œ¥] (g : Œ≤ ‚Üíco Œ≥) : C_c(Œ≥, Œ¥) ‚Üí‚Çô* C_c(Œ≤, Œ¥) where
  toFun f := f.comp g
  map_mul' _ _ := rfl

/-- Composition as a linear map. -/
def compLinearMap [AddCommMonoid Œ¥] [ContinuousAdd Œ¥] {R : Type*} [Semiring R] [Module R Œ¥]
    [ContinuousConstSMul R Œ¥] (g : Œ≤ ‚Üíco Œ≥) : C_c(Œ≥, Œ¥) ‚Üí‚Çó[R] C_c(Œ≤, Œ¥) where
  toFun f := f.comp g
  map_add' _ _ := rfl
  map_smul' _ _ := rfl

/-- Composition as a non-unital algebra homomorphism. -/
def compNonUnitalAlgHom {R : Type*} [Semiring R] [NonUnitalNonAssocSemiring Œ¥]
    [TopologicalSemiring Œ¥] [Module R Œ¥] [ContinuousConstSMul R Œ¥] (g : Œ≤ ‚Üíco Œ≥) :
    C_c(Œ≥, Œ¥) ‚Üí‚Çô‚Çê[R] C_c(Œ≤, Œ¥) where
  toFun f := f.comp g
  map_smul' _ _ := rfl
  map_zero' := rfl
  map_add' _ _ := rfl
  map_mul' _ _ := rfl

end CompactlySupportedContinuousMap

namespace CompactlySupportedContinuousMapClass

section Basic

variable [Zero Œ≤] [TopologicalSpace Œ≤] [FunLike F Œ± Œ≤] [CompactlySupportedContinuousMapClass F Œ± Œ≤]

instance : CoeTC F (CompactlySupportedContinuousMap Œ± Œ≤) :=
  ‚ü®fun f =>
    { toFun := f
      continuous_toFun := map_continuous f
      hasCompactSupport' := hasCompactSupport f }‚ü©

/-- A continuous function on a compact space has automatically compact support. This is not an
instance to avoid type class loops. -/
lemma of_compactSpace (G : Type*) [FunLike G Œ± Œ≤]
    [ContinuousMapClass G Œ± Œ≤] [CompactSpace Œ±] : CompactlySupportedContinuousMapClass G Œ± Œ≤ where
  map_continuous := map_continuous
  hasCompactSupport := by
    intro f
    exact HasCompactSupport.of_compactSpace f

end Basic

section Uniform

variable [UniformSpace Œ≤] [UniformSpace Œ≥] [Zero Œ≥] [FunLike F Œ≤ Œ≥]
  [CompactlySupportedContinuousMapClass F Œ≤ Œ≥]

theorem uniformContinuous (f : F) : UniformContinuous (f : Œ≤ ‚Üí Œ≥) :=
  (map_continuous f).uniformContinuous_of_tendsto_cocompact
  (HasCompactSupport.is_zero_at_infty (hasCompactSupport f))

end Uniform

section ZeroAtInfty

variable [TopologicalSpace Œ≤] [TopologicalSpace Œ≥] [Zero Œ≥]
  [FunLike F Œ≤ Œ≥] [CompactlySupportedContinuousMapClass F Œ≤ Œ≥]

instance : ZeroAtInftyContinuousMapClass F Œ≤ Œ≥ where
  zero_at_infty f := HasCompactSupport.is_zero_at_infty (hasCompactSupport f)

end ZeroAtInfty

end CompactlySupportedContinuousMapClass

section NonnegativePart

open NNReal

namespace CompactlySupportedContinuousMap

/-- The nonnegative part of a bounded continuous `‚Ñù`-valued function as a bounded
continuous `‚Ñù‚â•0`-valued function. -/
noncomputable def nnrealPart (f : C_c(Œ±, ‚Ñù)) : C_c(Œ±, ‚Ñù‚â•0) where
  toFun := Real.toNNReal.comp f.toFun
  continuous_toFun := Continuous.comp continuous_real_toNNReal f.continuous
  hasCompactSupport' := by
    apply HasCompactSupport.comp_left f.hasCompactSupport' Real.toNNReal_zero

@[simp]
lemma nnrealPart_apply (f : C_c(Œ±, ‚Ñù)) (x : Œ±) :
    f.nnrealPart x = Real.toNNReal (f x) := rfl

lemma nnrealPart_neg_eq_zero_of_nonneg (f : C_c(Œ±, ‚Ñù)) (hf : 0 ‚â§ f.1) : (-f).nnrealPart = 0 := by
  ext x
  simp only [nnrealPart_apply, coe_neg, Pi.neg_apply, Real.coe_toNNReal', coe_zero, Pi.zero_apply,
    NNReal.coe_zero, sup_eq_right, Left.neg_nonpos_iff]
  exact hf x

lemma nnrealPart_smul_pos (f : C_c(Œ±, ‚Ñù)) (a : ‚Ñù) (ha : 0 < a):
    (a ‚Ä¢ f).nnrealPart = a.toNNReal ‚Ä¢ f.nnrealPart := by
  ext x
  simp only [nnrealPart_apply, coe_smul, Pi.smul_apply, Real.coe_toNNReal', smul_eq_mul,
    NNReal.coe_mul]
  by_cases hfx : 0 ‚â§ f x
  ¬∑ rw [sup_of_le_left _, sup_of_le_left hfx]
    ¬∑ rw [sup_of_le_left (le_of_lt ha)]
    ¬∑ exact mul_nonneg (le_of_lt ha) hfx
  ¬∑ push_neg at hfx
    rw [sup_of_le_right, sup_of_le_right (le_of_lt hfx)]
    ¬∑ simp only [mul_zero]
    apply mul_nonpos_iff.mpr
    left
    exact ‚ü®(le_of_lt ha), le_of_lt hfx‚ü©

lemma nnrealPart_smul_neg (f : C_c(Œ±, ‚Ñù)) (a : ‚Ñù) (ha : a < 0):
    (a ‚Ä¢ f).nnrealPart = (-a).toNNReal ‚Ä¢ (-f).nnrealPart := by
  ext x
  simp only [nnrealPart_apply, coe_smul, Pi.smul_apply, smul_eq_mul, Real.coe_toNNReal', coe_neg,
    Pi.neg_apply, NNReal.coe_mul]
  by_cases hfx : 0 ‚â§ f x
  ¬∑ rw [sup_of_le_right _, sup_of_le_left (neg_nonneg.mpr (le_of_lt ha)),
      sup_of_le_right (neg_nonpos.mpr hfx)]
    ¬∑ simp only [mul_zero]
    apply mul_nonpos_iff.mpr
    right
    exact ‚ü®le_of_lt ha, hfx‚ü©
  ¬∑ push_neg at hfx
    rw [sup_of_le_left _, sup_of_le_left (neg_nonneg.mpr (le_of_lt ha)),
      sup_of_le_left (neg_nonneg.mpr (le_of_lt hfx))]
    ¬∑ ring
    exact mul_nonneg_of_nonpos_of_nonpos (le_of_lt ha) (le_of_lt hfx)

lemma add_nnrealPart_le_nnrealPart_add (f g : C_c(Œ±, ‚Ñù)) :
    (f + g).nnrealPart.1 ‚â§ f.nnrealPart.1 + g.nnrealPart.1 := by
  intro x
  simp only [ContinuousMap.toFun_eq_coe, coe_toContinuousMap, nnrealPart_apply, coe_add,
    Pi.add_apply, ContinuousMap.add_apply]
  exact Real.toNNReal_add_le

lemma exist_add_eq (f‚ÇÅ f‚ÇÇ : C_c(Œ±, ‚Ñù‚â•0)) (h : f‚ÇÅ.1 ‚â§ f‚ÇÇ.1)  : ‚àÉ (g : C_c(Œ±, ‚Ñù‚â•0)), f‚ÇÅ + g = f‚ÇÇ := by
  set g_cf := f‚ÇÇ.1 - f‚ÇÅ.1 with hg_cf
  have g_cp : HasCompactSupport g_cf := by
    apply IsCompact.of_isClosed_subset
      (IsCompact.union f‚ÇÅ.hasCompactSupport' f‚ÇÇ.hasCompactSupport') isClosed_closure
    rw [tsupport, tsupport, ‚Üê closure_union]
    apply closure_mono
    intro x
    simp only [Function.mem_support, ne_eq, ContinuousMap.toFun_eq_coe,
      CompactlySupportedContinuousMap.coe_toContinuousMap, Set.mem_union, hg_cf,
      ContinuousMap.sub_apply, CompactlySupportedContinuousMap.coe_toContinuousMap]
    by_contra!
    rw [this.2.1, this.2.2] at this
    simp only [tsub_self, ne_eq, not_true_eq_false, and_self, and_true] at this
  use ‚ü®g_cf, g_cp‚ü©
  ext x
  simp only [CompactlySupportedContinuousMap.coe_add, CompactlySupportedContinuousMap.coe_mk,
    Pi.add_apply, NNReal.coe_inj, hg_cf, ContinuousMap.sub_apply,
    CompactlySupportedContinuousMap.coe_toContinuousMap]
  exact add_tsub_cancel_of_le (h x)

lemma exist_add_nnrealPart_add_eq (f g : C_c(Œ±, ‚Ñù)) : ‚àÉ (h : C_c(Œ±, ‚Ñù‚â•0)),
    (f + g).nnrealPart + h = f.nnrealPart + g.nnrealPart ‚àß
    (-f + -g).nnrealPart + h = (-f).nnrealPart + (-g).nnrealPart := by
  obtain ‚ü®h, hh‚ü© := exist_add_eq (f + g).nnrealPart (f.nnrealPart + g.nnrealPart)
    (add_nnrealPart_le_nnrealPart_add f g)
  use h
  refine ‚ü®hh, ?_‚ü©
  ext x
  simp only [coe_add, Pi.add_apply, nnrealPart_apply, coe_neg, Pi.neg_apply, NNReal.coe_add,
    Real.coe_toNNReal']
  have hhx : (f x + g x) ‚äî 0 + ‚Üë(h x) = f x ‚äî 0 + g x ‚äî 0 := by
    have hhx' : ((f + g).nnrealPart + h) x = (f.nnrealPart + g.nnrealPart) x := by
      congr
    simp only [coe_add, Pi.add_apply, nnrealPart_apply, Real.coe_toNNReal'] at hhx'
    rw [‚Üê Real.coe_toNNReal', ‚Üê Real.coe_toNNReal', ‚Üê Real.coe_toNNReal', ‚Üê NNReal.coe_add,
      ‚Üê NNReal.coe_add]
    exact congrArg toReal hhx'
  rw [‚Üê neg_add]
  by_cases hfx : 0 ‚â§ f x
  ¬∑ by_cases hgx : 0 ‚â§ g x
    ¬∑ rw [sup_eq_left.mpr hfx, sup_eq_left.mpr hgx, sup_eq_left.mpr (add_nonneg hfx hgx)] at hhx
      simp only [add_right_eq_self, coe_eq_zero] at hhx
      rw [sup_eq_right.mpr (neg_nonpos.mpr hfx), sup_eq_right.mpr (neg_nonpos.mpr hgx),
        sup_eq_right.mpr (neg_nonpos.mpr (add_nonneg hfx hgx))]
      simp only [zero_add, add_zero, coe_eq_zero]
      exact hhx
    ¬∑ push_neg at hgx
      by_cases hfgx : 0 ‚â§ f x + g x
      ¬∑ rw [sup_eq_left.mpr hfx, sup_eq_right.mpr (le_of_lt hgx), sup_eq_left.mpr hfgx] at hhx
        simp only [add_zero, add_assoc, add_right_eq_self] at hhx
        rw [sup_eq_right.mpr (neg_nonpos.mpr hfx), sup_eq_left.mpr (le_of_lt (neg_pos.mpr hgx)),
          sup_eq_right.mpr (neg_nonpos.mpr hfgx)]
        ring_nf
        exact Eq.symm (neg_eq_of_add_eq_zero_right hhx)
      ¬∑ push_neg at hfgx
        rw [sup_eq_left.mpr hfx, sup_eq_right.mpr (le_of_lt hgx), sup_eq_right.mpr (le_of_lt hfgx)]
          at hhx
        simp only [zero_add, add_zero] at hhx
        rw [sup_eq_right.mpr (neg_nonpos.mpr hfx), sup_eq_left.mpr (le_of_lt (neg_pos.mpr hgx)),
          sup_eq_left.mpr (neg_nonneg.mpr (le_of_lt hfgx)), hhx]
        ring
  ¬∑ push_neg at hfx
    by_cases hgx : 0 ‚â§ g x
    ¬∑ by_cases hfgx : 0 ‚â§ f x + g x
      ¬∑ rw [sup_eq_right.mpr (le_of_lt hfx), sup_eq_left.mpr hgx, sup_eq_left.mpr hfgx, zero_add,
          add_comm, ‚Üê add_assoc] at hhx
        simp only [add_left_eq_self] at hhx
        rw [sup_eq_left.mpr (neg_nonneg.mpr (le_of_lt hfx)), sup_eq_right.mpr (neg_nonpos.mpr hgx),
          sup_eq_right.mpr (neg_nonpos.mpr hfgx)]
        simp only [zero_add, add_zero]
        exact eq_neg_of_add_eq_zero_left hhx
      ¬∑ push_neg at hfgx
        rw [sup_eq_right.mpr (le_of_lt hfx), sup_eq_left.mpr hgx, sup_eq_right.mpr (le_of_lt hfgx),
          zero_add, add_comm, add_zero] at hhx
        rw [sup_eq_left.mpr (neg_nonneg.mpr (le_of_lt hfx)), sup_eq_right.mpr (neg_nonpos.mpr hgx),
          sup_eq_left.mpr (neg_nonneg.mpr (le_of_lt hfgx)), hhx]
        simp only [neg_add_rev, neg_add_cancel_comm, add_zero]
    ¬∑ push_neg at hgx
      rw [sup_eq_right.mpr (le_of_lt hfx), sup_eq_right.mpr (le_of_lt hgx),
        sup_eq_right.mpr (le_of_lt (add_neg hfx hgx))] at hhx
      simp only [zero_add, add_zero, coe_eq_zero] at hhx
      rw [sup_eq_left.mpr (neg_nonneg.mpr (le_of_lt hfx)),
        sup_eq_left.mpr (le_of_lt (neg_pos.mpr hgx)),
        sup_eq_left.mpr (neg_nonneg.mpr (le_of_lt (add_neg hfx hgx))), hhx]
      simp only [neg_add_rev, NNReal.coe_zero, add_zero]
      exact AddCommMagma.add_comm (-g x) (-f x)

instance : Coe C(Œ±, ‚Ñù‚â•0) C(Œ±, ‚Ñù) where
  coe := fun f => ‚ü®fun x => NNReal.toReal (f.toFun x), by continuity‚ü©

instance : Coe C_c(Œ±, ‚Ñù‚â•0) C_c(Œ±, ‚Ñù) where
  coe := fun f => ‚ü®f.toContinuousMap,
                  by
                  simp only [ContinuousMap.toFun_eq_coe,
                    CompactlySupportedContinuousMap.coe_toContinuousMap]
                  apply HasCompactSupport.comp_left f.hasCompactSupport' rfl‚ü©

lemma eq_nnrealPart_neg_nnrealPart (f : C_c(Œ±, ‚Ñù)) : f = nnrealPart f - nnrealPart (-f) := by
  ext x
  simp only [ContinuousMap.toFun_eq_coe, CompactlySupportedContinuousMap.coe_toContinuousMap,
    nnrealPart_apply, Real.coe_toNNReal', CompactlySupportedContinuousMap.coe_neg,
    Pi.neg_apply, CompactlySupportedContinuousMap.coe_sub, CompactlySupportedContinuousMap.coe_mk,
    ContinuousMap.coe_mk, Pi.sub_apply, max_zero_sub_max_neg_zero_eq_self]

/-- The compactly supported continuous `‚Ñù‚â•0`-valued function as a compactly supported `‚Ñù`-valued
function. -/
noncomputable def toReal : C_c(Œ±, ‚Ñù‚â•0) ‚Üí‚Çó[‚Ñù‚â•0] C_c(Œ±, ‚Ñù) where
  toFun := fun f => ‚ü®ContinuousMap.coeNNRealReal.comp f.1,
                    by
                    simp only [ContinuousMap.toFun_eq_coe, ContinuousMap.coe_comp,
                      ContinuousMap.coeNNRealReal_apply,
                      CompactlySupportedContinuousMap.coe_toContinuousMap]
                    exact HasCompactSupport.comp_left f.hasCompactSupport' (by rfl)‚ü©
  map_add' f g := by
    ext x
    simp only [CompactlySupportedContinuousMap.coe_mk, ContinuousMap.comp_apply,
      CompactlySupportedContinuousMap.coe_toContinuousMap, CompactlySupportedContinuousMap.coe_add,
      Pi.add_apply, ContinuousMap.coeNNRealReal_apply, NNReal.coe_add, ContinuousMap.coe_comp,
      Function.comp_apply]
  map_smul' a f := by
    ext x
    simp only [CompactlySupportedContinuousMap.coe_mk, ContinuousMap.comp_apply,
      CompactlySupportedContinuousMap.coe_toContinuousMap, CompactlySupportedContinuousMap.coe_smul,
      Pi.smul_apply, smul_eq_mul, ContinuousMap.coeNNRealReal_apply,
      RingHom.id_apply, ContinuousMap.coe_comp, Function.comp_apply]
    rw [NNReal.smul_def, smul_eq_mul]
    simp only [NNReal.coe_mul]

@[simp]
lemma toReal_apply (f : C_c(Œ±, ‚Ñù‚â•0)) (x : Œ±) :
    toReal f x = f x := rfl

lemma nnreal_coe_eq_toReal (f : C_c(Œ±, ‚Ñù‚â•0)) :
    (f : C_c(Œ±, ‚Ñù)) = toReal f := by
  ext x
  simp only [ContinuousMap.toFun_eq_coe, coe_toContinuousMap, coe_mk, ContinuousMap.coe_mk,
    toReal_apply]

@[simp]
lemma nnrealPart_toReal_eq (f : C_c(Œ±, ‚Ñù‚â•0)) : nnrealPart (toReal f) = f := by
  ext x
  simp only [nnrealPart_apply, toReal_apply, Real.toNNReal_coe]

@[simp]
lemma nnrealPart_neg_toReal_eq (f : C_c(Œ±, ‚Ñù‚â•0)) : nnrealPart (- toReal f) = 0 := by
  ext x
  simp only [nnrealPart_apply, coe_neg, Pi.neg_apply, toReal_apply, Real.coe_toNNReal',
    Left.neg_nonpos_iff, zero_le_coe, sup_of_le_right, coe_zero, Pi.zero_apply, NNReal.coe_zero]

section toNNRealLinear

/-- For a positive linear functional `Œõ : C_c(Œ±, ‚Ñù) ‚Üí ‚Ñù`, define a `‚Ñù‚â•0`-linear map. -/
noncomputable def toNNRealLinear {Œõ : C_c(Œ±, ‚Ñù) ‚Üí‚Çó[‚Ñù] ‚Ñù} (hŒõ : ‚àÄ f, 0 ‚â§ f.1 ‚Üí 0 ‚â§ Œõ f) :
    C_c(Œ±, ‚Ñù‚â•0) ‚Üí‚Çó[‚Ñù‚â•0] ‚Ñù‚â•0 where
  toFun := fun f => ‚ü®Œõ f,
                    by
                    apply hŒõ f
                    simp only [ContinuousMap.toFun_eq_coe,
                      CompactlySupportedContinuousMap.coe_toContinuousMap]
                    intro x
                    simp only [ContinuousMap.toFun_eq_coe, ContinuousMap.zero_apply, zero_le_coe]‚ü©
  map_add' f g := by
    simp_rw [nnreal_coe_eq_toReal f, nnreal_coe_eq_toReal g, nnreal_coe_eq_toReal (f + g)]
    simp only [map_add]
    exact rfl
  map_smul' a f := by
    simp_rw [nnreal_coe_eq_toReal f, nnreal_coe_eq_toReal (a ‚Ä¢ f)]
    simp only [map_smul, LinearMap.map_smul_of_tower, RingHom.id_apply, smul_eq_mul]
    exact rfl

@[simp]
lemma toNNRealLinear_apply {Œõ : C_c(Œ±, ‚Ñù) ‚Üí‚Çó[‚Ñù] ‚Ñù} (hŒõ : ‚àÄ f, 0 ‚â§ f.1 ‚Üí 0 ‚â§ Œõ f) (f : C_c(Œ±, ‚Ñù‚â•0)) :
    toNNRealLinear hŒõ f = (Œõ (toReal f)) := by rfl

lemma eq_toNNRealLinear_nnrealPart_sub {Œõ : C_c(Œ±, ‚Ñù) ‚Üí‚Çó[‚Ñù] ‚Ñù}
    (hŒõ : ‚àÄ f, 0 ‚â§ f.1 ‚Üí 0 ‚â§ Œõ f) (f : C_c(Œ±, ‚Ñù)) :
    Œõ f = toNNRealLinear hŒõ (nnrealPart f)
            - toNNRealLinear hŒõ (nnrealPart (-f)) := by
  simp only [toNNRealLinear_apply]
  rw [‚Üê LinearMap.map_sub, ‚Üê nnreal_coe_eq_toReal, ‚Üê nnreal_coe_eq_toReal]
  congr
  simp only [ContinuousMap.toFun_eq_coe, CompactlySupportedContinuousMap.coe_toContinuousMap,
    nnrealPart, Real.coe_toNNReal', CompactlySupportedContinuousMap.coe_neg, Pi.neg_apply]
  ext x
  simp only [ContinuousMap.coe_mk, Function.comp_apply, Real.coe_toNNReal', Pi.neg_apply, coe_sub,
    coe_mk, Pi.sub_apply, max_zero_sub_max_neg_zero_eq_self]

lemma toNNRealLinear_eq_iff {Œõ‚ÇÅ Œõ‚ÇÇ : C_c(Œ±, ‚Ñù) ‚Üí‚Çó[‚Ñù] ‚Ñù} (hŒõ‚ÇÅ : ‚àÄ f, 0 ‚â§ f.1 ‚Üí 0 ‚â§ Œõ‚ÇÅ f)
    (hŒõ‚ÇÇ : ‚àÄ f, 0 ‚â§ f.1 ‚Üí 0 ‚â§ Œõ‚ÇÇ f) : Œõ‚ÇÅ = Œõ‚ÇÇ ‚Üî toNNRealLinear hŒõ‚ÇÅ = toNNRealLinear hŒõ‚ÇÇ := by
  constructor
  ¬∑ intro h
    ext f
    simp only [toNNRealLinear_apply, Real.coe_toNNReal']
    exact congrFun (congrArg DFunLike.coe h) (toReal f)
  ¬∑ intro h
    ext f
    rw [eq_toNNRealLinear_nnrealPart_sub hŒõ‚ÇÅ, eq_toNNRealLinear_nnrealPart_sub hŒõ‚ÇÇ, h]

end toNNRealLinear

section toRealLinear

/-- For a positive linear functional `Œõ : C_c(Œ±, ‚Ñù‚â•0) ‚Üí ‚Ñù‚â•0`, define a `‚Ñù`-linear map. -/
noncomputable def toRealLinear (Œõ : C_c(Œ±, ‚Ñù‚â•0) ‚Üí‚Çó[‚Ñù‚â•0] ‚Ñù‚â•0) : C_c(Œ±, ‚Ñù) ‚Üí‚Çó[‚Ñù] ‚Ñù where
  toFun := fun f => Œõ (nnrealPart f) - Œõ (nnrealPart (- f))
  map_add' f g := by
    simp only [neg_add_rev]
    obtain ‚ü®h, hh‚ü© := exist_add_nnrealPart_add_eq f g
    rw [‚Üê add_zero ((Œõ (f + g).nnrealPart).toReal - (Œõ (-g + -f).nnrealPart).toReal),
      ‚Üê sub_self (Œõ h).toReal, sub_add_sub_comm, ‚Üê NNReal.coe_add, ‚Üê NNReal.coe_add,
      ‚Üê LinearMap.map_add, ‚Üê LinearMap.map_add, hh.1, add_comm (-g) (-f), hh.2]
    simp only [map_add, NNReal.coe_add]
    ring
  map_smul' a f := by
    apply Or.elim3 (lt_trichotomy 0 a)
    ¬∑ intro ha
      simp only [RingHom.id_apply, smul_eq_mul]
      rw [‚Üê (smul_neg a f)]
      rw [nnrealPart_smul_pos f a ha, nnrealPart_smul_pos (-f) a ha]
      simp only [map_smul, smul_eq_mul, NNReal.coe_mul, Real.coe_toNNReal']
      rw [sup_of_le_left (le_of_lt ha)]
      rw [mul_sub]
    ¬∑ intro ha
      rw [‚Üê ha]
      simp only [zero_smul, neg_zero, sub_self, RingHom.id_apply, smul_eq_mul, zero_mul]
    ¬∑ intro ha
      simp only [RingHom.id_apply, smul_eq_mul]
      rw [‚Üê (smul_neg a f)]
      rw [nnrealPart_smul_neg f a ha, nnrealPart_smul_neg (-f) a ha]
      simp only [map_smul, smul_eq_mul, NNReal.coe_mul, Real.coe_toNNReal', neg_neg]
      rw [sup_of_le_left (neg_nonneg.mpr (le_of_lt ha))]
      ring

lemma toRealLinear_apply {Œõ : C_c(Œ±, ‚Ñù‚â•0) ‚Üí‚Çó[‚Ñù‚â•0] ‚Ñù‚â•0} (f : C_c(Œ±, ‚Ñù)) :
    (toRealLinear Œõ) f = Œõ (nnrealPart f) - Œõ (nnrealPart (-f)) := by rfl

lemma nonneg_toRealLinear (Œõ : C_c(Œ±, ‚Ñù‚â•0) ‚Üí‚Çó[‚Ñù‚â•0] ‚Ñù‚â•0) (g : C_c(Œ±, ‚Ñù)) (hg : 0 ‚â§ g.1) :
    0 ‚â§ toRealLinear Œõ g := by
  rw [toRealLinear_apply]
  simp only [sub_nonneg, coe_le_coe]
  rw [nnrealPart_neg_eq_zero_of_nonneg g hg]
  simp only [map_zero, zero_le]

lemma eq_toRealLinear_toReal {Œõ : C_c(Œ±, ‚Ñù‚â•0) ‚Üí‚Çó[‚Ñù‚â•0] ‚Ñù‚â•0} (f : C_c(Œ±, ‚Ñù‚â•0)) :
    Œõ f = (toRealLinear Œõ) (toReal f) := by
  rw [toRealLinear_apply]
  simp only [nnrealPart_toReal_eq, nnrealPart_neg_toReal_eq, map_zero, NNReal.coe_zero, sub_zero]

lemma eq_toNNRealLinear_toRealLinear (Œõ : C_c(Œ±, ‚Ñù‚â•0) ‚Üí‚Çó[‚Ñù‚â•0] ‚Ñù‚â•0) :
    Œõ = (toNNRealLinear (nonneg_toRealLinear Œõ)) := by
  ext f
  rw [toNNRealLinear_apply]
  exact eq_toRealLinear_toReal f

end toRealLinear

end CompactlySupportedContinuousMap

end NonnegativePart
