/-
Copyright (c) 2018 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Mario Carneiro, Yury Kudryashov, Heather Macbeth
-/
import Mathlib.Topology.Algebra.Indicator
import Mathlib.Topology.Bornology.BoundedOperation
import Mathlib.Topology.ContinuousMap.Algebra

/-!
# Bounded continuous functions

The type of bounded continuous functions taking values in a metric space, with the uniform distance.
-/

assert_not_exists CStarRing

noncomputable section

open Topology Bornology NNReal UniformConvergence

open Set Filter Metric Function

universe u v w

variable {F : Type*} {Î± : Type u} {Î² : Type v} {Î³ : Type w}

/-- `Î± â†’áµ‡ Î²` is the type of bounded continuous functions `Î± â†’ Î²` from a topological space to a
metric space.

When possible, instead of parametrizing results over `(f : Î± â†’áµ‡ Î²)`,
you should parametrize over `(F : Type*) [BoundedContinuousMapClass F Î± Î²] (f : F)`.

When you extend this structure, make sure to extend `BoundedContinuousMapClass`. -/
structure BoundedContinuousFunction (Î± : Type u) (Î² : Type v) [TopologicalSpace Î±]
    [PseudoMetricSpace Î²] : Type max u v extends ContinuousMap Î± Î² where
  map_bounded' : âˆƒ C, âˆ€ x y, dist (toFun x) (toFun y) â‰¤ C

@[inherit_doc] scoped[BoundedContinuousFunction] infixr:25 " â†’áµ‡ " => BoundedContinuousFunction

section

/-- `BoundedContinuousMapClass F Î± Î²` states that `F` is a type of bounded continuous maps.

You should also extend this typeclass when you extend `BoundedContinuousFunction`. -/
class BoundedContinuousMapClass (F : Type*) (Î± Î² : outParam Type*) [TopologicalSpace Î±]
    [PseudoMetricSpace Î²] [FunLike F Î± Î²] : Prop extends ContinuousMapClass F Î± Î² where
  map_bounded (f : F) : âˆƒ C, âˆ€ x y, dist (f x) (f y) â‰¤ C

end

export BoundedContinuousMapClass (map_bounded)

namespace BoundedContinuousFunction

section Basics

variable [TopologicalSpace Î±] [PseudoMetricSpace Î²] [PseudoMetricSpace Î³]
variable {f g : Î± â†’áµ‡ Î²} {x : Î±} {C : â„}

instance instFunLike : FunLike (Î± â†’áµ‡ Î²) Î± Î² where
  coe f := f.toFun
  coe_injective' f g h := by
    obtain âŸ¨âŸ¨_, _âŸ©, _âŸ© := f
    obtain âŸ¨âŸ¨_, _âŸ©, _âŸ© := g
    congr

instance instBoundedContinuousMapClass : BoundedContinuousMapClass (Î± â†’áµ‡ Î²) Î± Î² where
  map_continuous f := f.continuous_toFun
  map_bounded f := f.map_bounded'

instance instCoeTC [FunLike F Î± Î²] [BoundedContinuousMapClass F Î± Î²] : CoeTC F (Î± â†’áµ‡ Î²) :=
  âŸ¨fun f â†¦
    { toFun := f
      continuous_toFun := map_continuous f
      map_bounded' := map_bounded f }âŸ©

@[simp]
theorem coe_toContinuousMap (f : Î± â†’áµ‡ Î²) : (f.toContinuousMap : Î± â†’ Î²) = f := rfl

/-- See Note [custom simps projection]. We need to specify this projection explicitly in this case,
  because it is a composition of multiple projections. -/
def Simps.apply (h : Î± â†’áµ‡ Î²) : Î± â†’ Î² := h

initialize_simps_projections BoundedContinuousFunction (toFun â†’ apply)

protected theorem bounded (f : Î± â†’áµ‡ Î²) : âˆƒ C, âˆ€ x y : Î±, dist (f x) (f y) â‰¤ C :=
  f.map_bounded'

protected theorem continuous (f : Î± â†’áµ‡ Î²) : Continuous f :=
  f.toContinuousMap.continuous

@[ext]
theorem ext (h : âˆ€ x, f x = g x) : f = g :=
  DFunLike.ext _ _ h

theorem isBounded_range (f : Î± â†’áµ‡ Î²) : IsBounded (range f) :=
  isBounded_range_iff.2 f.bounded

theorem isBounded_image (f : Î± â†’áµ‡ Î²) (s : Set Î±) : IsBounded (f '' s) :=
  f.isBounded_range.subset <| image_subset_range _ _

theorem eq_of_empty [h : IsEmpty Î±] (f g : Î± â†’áµ‡ Î²) : f = g :=
  ext <| h.elim

/-- A continuous function with an explicit bound is a bounded continuous function. -/
def mkOfBound (f : C(Î±, Î²)) (C : â„) (h : âˆ€ x y : Î±, dist (f x) (f y) â‰¤ C) : Î± â†’áµ‡ Î² :=
  âŸ¨f, âŸ¨C, hâŸ©âŸ©

@[simp]
theorem mkOfBound_coe {f} {C} {h} : (mkOfBound f C h : Î± â†’ Î²) = (f : Î± â†’ Î²) := rfl

/-- A continuous function on a compact space is automatically a bounded continuous function. -/
def mkOfCompact [CompactSpace Î±] (f : C(Î±, Î²)) : Î± â†’áµ‡ Î² :=
  âŸ¨f, isBounded_range_iff.1 (isCompact_range f.continuous).isBoundedâŸ©

@[simp]
theorem mkOfCompact_apply [CompactSpace Î±] (f : C(Î±, Î²)) (a : Î±) : mkOfCompact f a = f a := rfl

/-- If a function is bounded on a discrete space, it is automatically continuous,
and therefore gives rise to an element of the type of bounded continuous functions. -/
@[simps]
def mkOfDiscrete [DiscreteTopology Î±] (f : Î± â†’ Î²) (C : â„) (h : âˆ€ x y : Î±, dist (f x) (f y) â‰¤ C) :
    Î± â†’áµ‡ Î² :=
  âŸ¨âŸ¨f, continuous_of_discreteTopologyâŸ©, âŸ¨C, hâŸ©âŸ©

/-- The uniform distance between two bounded continuous functions. -/
instance instDist : Dist (Î± â†’áµ‡ Î²) :=
  âŸ¨fun f g â†¦ sInf { C | 0 â‰¤ C âˆ§ âˆ€ x : Î±, dist (f x) (g x) â‰¤ C }âŸ©

theorem dist_eq : dist f g = sInf { C | 0 â‰¤ C âˆ§ âˆ€ x : Î±, dist (f x) (g x) â‰¤ C } := rfl

theorem dist_set_exists : âˆƒ C, 0 â‰¤ C âˆ§ âˆ€ x : Î±, dist (f x) (g x) â‰¤ C := by
  rcases isBounded_iff.1 (f.isBounded_range.union g.isBounded_range) with âŸ¨C, hCâŸ©
  refine âŸ¨max 0 C, le_max_left _ _, fun x â†¦ (hC ?_ ?_).trans (le_max_right _ _)âŸ©
    <;> [left; right]
    <;> apply mem_range_self

/-- The pointwise distance is controlled by the distance between functions, by definition. -/
theorem dist_coe_le_dist (x : Î±) : dist (f x) (g x) â‰¤ dist f g :=
  le_csInf dist_set_exists fun _ hb â†¦ hb.2 x

/- This lemma will be needed in the proof of the metric space instance, but it will become
useless afterwards as it will be superseded by the general result that the distance is nonnegative
in metric spaces. -/
private theorem dist_nonneg' : 0 â‰¤ dist f g :=
  le_csInf dist_set_exists fun _ â†¦ And.left

/-- The distance between two functions is controlled by the supremum of the pointwise distances. -/
theorem dist_le (C0 : (0 : â„) â‰¤ C) : dist f g â‰¤ C â†” âˆ€ x : Î±, dist (f x) (g x) â‰¤ C :=
  âŸ¨fun h x â†¦ le_trans (dist_coe_le_dist x) h, fun H â†¦ csInf_le âŸ¨0, fun _ â†¦ And.leftâŸ© âŸ¨C0, HâŸ©âŸ©

theorem dist_le_iff_of_nonempty [Nonempty Î±] : dist f g â‰¤ C â†” âˆ€ x, dist (f x) (g x) â‰¤ C :=
  âŸ¨fun h x â†¦ le_trans (dist_coe_le_dist x) h,
    fun w â†¦ (dist_le (le_trans dist_nonneg (w (Nonempty.some â€¹_â€º)))).mpr wâŸ©

theorem dist_lt_of_nonempty_compact [Nonempty Î±] [CompactSpace Î±]
    (w : âˆ€ x : Î±, dist (f x) (g x) < C) : dist f g < C := by
  have c : Continuous fun x â†¦ dist (f x) (g x) := by fun_prop
  obtain âŸ¨x, -, leâŸ© :=
    IsCompact.exists_isMaxOn isCompact_univ Set.univ_nonempty (Continuous.continuousOn c)
  exact lt_of_le_of_lt (dist_le_iff_of_nonempty.mpr fun y â†¦ le trivial) (w x)

theorem dist_lt_iff_of_compact [CompactSpace Î±] (C0 : (0 : â„) < C) :
    dist f g < C â†” âˆ€ x : Î±, dist (f x) (g x) < C := by
  fconstructor
  Â· intro w x
    exact lt_of_le_of_lt (dist_coe_le_dist x) w
  Â· by_cases h : Nonempty Î±
    Â· exact dist_lt_of_nonempty_compact
    Â· rintro -
      convert C0
      apply le_antisymm _ dist_nonneg'
      rw [dist_eq]
      exact csInf_le âŸ¨0, fun C â†¦ And.leftâŸ© âŸ¨le_rfl, fun x â†¦ False.elim (h (Nonempty.intro x))âŸ©

theorem dist_lt_iff_of_nonempty_compact [Nonempty Î±] [CompactSpace Î±] :
    dist f g < C â†” âˆ€ x : Î±, dist (f x) (g x) < C :=
  âŸ¨fun w x â†¦ lt_of_le_of_lt (dist_coe_le_dist x) w, dist_lt_of_nonempty_compactâŸ©

/-- The type of bounded continuous functions, with the uniform distance, is a pseudometric space. -/
instance instPseudoMetricSpace : PseudoMetricSpace (Î± â†’áµ‡ Î²) where
  dist_self f := le_antisymm ((dist_le le_rfl).2 fun x â†¦ by simp) dist_nonneg'
  dist_comm f g := by simp [dist_eq, dist_comm]
  dist_triangle _ _ _ := (dist_le (add_nonneg dist_nonneg' dist_nonneg')).2
    fun _ â†¦ le_trans (dist_triangle _ _ _) (add_le_add (dist_coe_le_dist _) (dist_coe_le_dist _))

/-- The type of bounded continuous functions, with the uniform distance, is a metric space. -/
instance instMetricSpace {Î²} [MetricSpace Î²] : MetricSpace (Î± â†’áµ‡ Î²) where
  eq_of_dist_eq_zero hfg := by
    ext x
    exact eq_of_dist_eq_zero (le_antisymm (hfg â–¸ dist_coe_le_dist _) dist_nonneg)

theorem nndist_eq : nndist f g = sInf { C | âˆ€ x : Î±, nndist (f x) (g x) â‰¤ C } :=
  Subtype.ext <| dist_eq.trans <| by
    rw [val_eq_coe, coe_sInf, coe_image]
    simp_rw [mem_setOf_eq, â† NNReal.coe_le_coe, coe_mk, exists_prop, coe_nndist]

theorem nndist_set_exists : âˆƒ C, âˆ€ x : Î±, nndist (f x) (g x) â‰¤ C :=
  Subtype.exists.mpr <| dist_set_exists.imp fun _ âŸ¨ha, hâŸ© â†¦ âŸ¨ha, hâŸ©

theorem nndist_coe_le_nndist (x : Î±) : nndist (f x) (g x) â‰¤ nndist f g :=
  dist_coe_le_dist x

/-- On an empty space, bounded continuous functions are at distance 0. -/
theorem dist_zero_of_empty [IsEmpty Î±] : dist f g = 0 := by
  rw [(ext isEmptyElim : f = g), dist_self]

theorem dist_eq_iSup : dist f g = â¨† x : Î±, dist (f x) (g x) := by
  cases isEmpty_or_nonempty Î±
  Â· rw [iSup_of_empty', Real.sSup_empty, dist_zero_of_empty]
  refine (dist_le_iff_of_nonempty.mpr <| le_ciSup ?_).antisymm (ciSup_le dist_coe_le_dist)
  exact dist_set_exists.imp fun C hC â†¦ forall_mem_range.2 hC.2

theorem nndist_eq_iSup : nndist f g = â¨† x : Î±, nndist (f x) (g x) :=
  Subtype.ext <| dist_eq_iSup.trans <| by simp_rw [val_eq_coe, coe_iSup, coe_nndist]

theorem edist_eq_iSup : edist f g = â¨† x, edist (f x) (g x) := by
  simp_rw [edist_nndist, nndist_eq_iSup]
  refine ENNReal.coe_iSup âŸ¨nndist f g, ?_âŸ©
  rintro - âŸ¨x, hx, rflâŸ©
  exact nndist_coe_le_nndist x

theorem tendsto_iff_tendstoUniformly {Î¹ : Type*} {F : Î¹ â†’ Î± â†’áµ‡ Î²} {f : Î± â†’áµ‡ Î²} {l : Filter Î¹} :
    Tendsto F l (ğ“ f) â†” TendstoUniformly (fun i â†¦ F i) f l :=
  Iff.intro
    (fun h â†¦
      tendstoUniformly_iff.2 fun Îµ Îµ0 â†¦
        (Metric.tendsto_nhds.mp h Îµ Îµ0).mp
          (Eventually.of_forall fun n hn x â†¦
            lt_of_le_of_lt (dist_coe_le_dist x) (dist_comm (F n) f â–¸ hn)))
    fun h â†¦
    Metric.tendsto_nhds.mpr fun _ Îµ_pos â†¦
      (h _ (dist_mem_uniformity <| half_pos Îµ_pos)).mp
        (Eventually.of_forall fun n hn â†¦
          lt_of_le_of_lt
            ((dist_le (half_pos Îµ_pos).le).mpr fun x â†¦ dist_comm (f x) (F n x) â–¸ le_of_lt (hn x))
            (half_lt_self Îµ_pos))

/-- The topology on `Î± â†’áµ‡ Î²` is exactly the topology induced by the natural map to `Î± â†’áµ¤ Î²`. -/
theorem isInducing_coeFn : IsInducing (UniformFun.ofFun âˆ˜ (â‡‘) : (Î± â†’áµ‡ Î²) â†’ Î± â†’áµ¤ Î²) := by
  rw [isInducing_iff_nhds]
  refine fun f â†¦ eq_of_forall_le_iff fun l â†¦ ?_
  rw [â† tendsto_iff_comap, â† tendsto_id', tendsto_iff_tendstoUniformly,
    UniformFun.tendsto_iff_tendstoUniformly]
  simp [comp_def]

-- TODO: upgrade to `IsUniformEmbedding`
theorem isEmbedding_coeFn : IsEmbedding (UniformFun.ofFun âˆ˜ (â‡‘) : (Î± â†’áµ‡ Î²) â†’ Î± â†’áµ¤ Î²) :=
  âŸ¨isInducing_coeFn, fun _ _ h â†¦ ext fun x â†¦ congr_fun h xâŸ©

variable (Î±) in
/-- Constant as a continuous bounded function. -/
@[simps! -fullyApplied]
def const (b : Î²) : Î± â†’áµ‡ Î² :=
  âŸ¨ContinuousMap.const Î± b, 0, by simpâŸ©

theorem const_apply' (a : Î±) (b : Î²) : (const Î± b : Î± â†’ Î²) a = b := rfl

/-- If the target space is inhabited, so is the space of bounded continuous functions. -/
instance [Inhabited Î²] : Inhabited (Î± â†’áµ‡ Î²) :=
  âŸ¨const Î± defaultâŸ©

theorem lipschitz_evalx (x : Î±) : LipschitzWith 1 fun f : Î± â†’áµ‡ Î² â†¦ f x :=
  LipschitzWith.mk_one fun _ _ â†¦ dist_coe_le_dist x

theorem uniformContinuous_coe : @UniformContinuous (Î± â†’áµ‡ Î²) (Î± â†’ Î²) _ _ (â‡‘) :=
  uniformContinuous_pi.2 fun x â†¦ (lipschitz_evalx x).uniformContinuous

theorem continuous_coe : Continuous fun (f : Î± â†’áµ‡ Î²) x â†¦ f x :=
  UniformContinuous.continuous uniformContinuous_coe

/-- When `x` is fixed, `(f : Î± â†’áµ‡ Î²) â†¦ f x` is continuous. -/
@[continuity]
theorem continuous_eval_const {x : Î±} : Continuous fun f : Î± â†’áµ‡ Î² â†¦ f x :=
  (continuous_apply x).comp continuous_coe

/-- The evaluation map is continuous, as a joint function of `u` and `x`. -/
@[continuity]
theorem continuous_eval : Continuous fun p : (Î± â†’áµ‡ Î²) Ã— Î± â†¦ p.1 p.2 :=
  (continuous_prod_of_continuous_lipschitzWith _ 1 fun f â†¦ f.continuous) <| lipschitz_evalx

/-- Bounded continuous functions taking values in a complete space form a complete space. -/
instance instCompleteSpace [CompleteSpace Î²] : CompleteSpace (Î± â†’áµ‡ Î²) :=
  complete_of_cauchySeq_tendsto fun (f : â„• â†’ Î± â†’áµ‡ Î²) (hf : CauchySeq f) â†¦ by
    /- We have to show that `f n` converges to a bounded continuous function.
      For this, we prove pointwise convergence to define the limit, then check
      it is a continuous bounded function, and then check the norm convergence. -/
    rcases cauchySeq_iff_le_tendsto_0.1 hf with âŸ¨b, b0, b_bound, b_limâŸ©
    have f_bdd := fun x n m N hn hm â†¦ le_trans (dist_coe_le_dist x) (b_bound n m N hn hm)
    have fx_cau : âˆ€ x, CauchySeq fun n â†¦ f n x :=
      fun x â†¦ cauchySeq_iff_le_tendsto_0.2 âŸ¨b, b0, f_bdd x, b_limâŸ©
    choose F hF using fun x â†¦ cauchySeq_tendsto_of_complete (fx_cau x)
    /- `F : Î± â†’ Î²`, `hF : âˆ€ (x : Î±), Tendsto (fun n â†¦ â†‘(f n) x) atTop (ğ“ (F x))`
      `F` is the desired limit function. Check that it is uniformly approximated by `f N`. -/
    have fF_bdd : âˆ€ x N, dist (f N x) (F x) â‰¤ b N :=
      fun x N â†¦ le_of_tendsto (tendsto_const_nhds.dist (hF x))
        (Filter.eventually_atTop.2 âŸ¨N, fun n hn â†¦ f_bdd x N n N (le_refl N) hnâŸ©)
    refine âŸ¨âŸ¨âŸ¨F, ?_âŸ©, ?_âŸ©, ?_âŸ©
    Â· -- Check that `F` is continuous, as a uniform limit of continuous functions
      have : TendstoUniformly (fun n x â†¦ f n x) F atTop := by
        refine Metric.tendstoUniformly_iff.2 fun Îµ Îµ0 â†¦ ?_
        refine ((tendsto_order.1 b_lim).2 Îµ Îµ0).mono fun n hn x â†¦ ?_
        rw [dist_comm]
        exact lt_of_le_of_lt (fF_bdd x n) hn
      exact this.continuous (Eventually.of_forall fun N â†¦ (f N).continuous)
    Â· -- Check that `F` is bounded
      rcases (f 0).bounded with âŸ¨C, hCâŸ©
      refine âŸ¨C + (b 0 + b 0), fun x y â†¦ ?_âŸ©
      calc
        dist (F x) (F y) â‰¤ dist (f 0 x) (f 0 y) + (dist (f 0 x) (F x) + dist (f 0 y) (F y)) :=
          dist_triangle4_left _ _ _ _
        _ â‰¤ C + (b 0 + b 0) := add_le_add (hC x y) (add_le_add (fF_bdd x 0) (fF_bdd y 0))
    Â· -- Check that `F` is close to `f N` in distance terms
      refine tendsto_iff_dist_tendsto_zero.2 (squeeze_zero (fun _ â†¦ dist_nonneg) ?_ b_lim)
      exact fun N â†¦ (dist_le (b0 _)).2 fun x â†¦ fF_bdd x N

/-- Composition of a bounded continuous function and a continuous function. -/
def compContinuous {Î´ : Type*} [TopologicalSpace Î´] (f : Î± â†’áµ‡ Î²) (g : C(Î´, Î±)) : Î´ â†’áµ‡ Î² where
  toContinuousMap := f.1.comp g
  map_bounded' := f.map_bounded'.imp fun _ hC _ _ â†¦ hC _ _

@[simp]
theorem coe_compContinuous {Î´ : Type*} [TopologicalSpace Î´] (f : Î± â†’áµ‡ Î²) (g : C(Î´, Î±)) :
    â‡‘(f.compContinuous g) = f âˆ˜ g := rfl

@[simp]
theorem compContinuous_apply {Î´ : Type*} [TopologicalSpace Î´] (f : Î± â†’áµ‡ Î²) (g : C(Î´, Î±)) (x : Î´) :
    f.compContinuous g x = f (g x) := rfl

theorem lipschitz_compContinuous {Î´ : Type*} [TopologicalSpace Î´] (g : C(Î´, Î±)) :
    LipschitzWith 1 fun f : Î± â†’áµ‡ Î² â†¦ f.compContinuous g :=
  LipschitzWith.mk_one fun _ _ â†¦ (dist_le dist_nonneg).2 fun x â†¦ dist_coe_le_dist (g x)

theorem continuous_compContinuous {Î´ : Type*} [TopologicalSpace Î´] (g : C(Î´, Î±)) :
    Continuous fun f : Î± â†’áµ‡ Î² â†¦ f.compContinuous g :=
  (lipschitz_compContinuous g).continuous

/-- Restrict a bounded continuous function to a set. -/
def restrict (f : Î± â†’áµ‡ Î²) (s : Set Î±) : s â†’áµ‡ Î² :=
  f.compContinuous <| (ContinuousMap.id _).restrict s

@[simp]
theorem coe_restrict (f : Î± â†’áµ‡ Î²) (s : Set Î±) : â‡‘(f.restrict s) = f âˆ˜ (â†‘) := rfl

@[simp]
theorem restrict_apply (f : Î± â†’áµ‡ Î²) (s : Set Î±) (x : s) : f.restrict s x = f x := rfl

/-- Composition (in the target) of a bounded continuous function with a Lipschitz map again
gives a bounded continuous function. -/
def comp (G : Î² â†’ Î³) {C : â„â‰¥0} (H : LipschitzWith C G) (f : Î± â†’áµ‡ Î²) : Î± â†’áµ‡ Î³ :=
  âŸ¨âŸ¨fun x â†¦ G (f x), H.continuous.comp f.continuousâŸ©,
    let âŸ¨D, hDâŸ© := f.bounded
    âŸ¨max C 0 * D, fun x y â†¦
      calc
        dist (G (f x)) (G (f y)) â‰¤ C * dist (f x) (f y) := H.dist_le_mul _ _
        _ â‰¤ max C 0 * dist (f x) (f y) := by gcongr; apply le_max_left
        _ â‰¤ max C 0 * D := by gcongr; apply hD
        âŸ©âŸ©

@[simp]
theorem comp_apply (G : Î² â†’ Î³) {C : â„â‰¥0} (H : LipschitzWith C G) (f : Î± â†’áµ‡ Î²) (a : Î±) :
    (f.comp G H) a = G (f a) := rfl

/-- The composition operator (in the target) with a Lipschitz map is Lipschitz. -/
theorem lipschitz_comp {G : Î² â†’ Î³} {C : â„â‰¥0} (H : LipschitzWith C G) :
    LipschitzWith C (comp G H : (Î± â†’áµ‡ Î²) â†’ Î± â†’áµ‡ Î³) :=
  LipschitzWith.of_dist_le_mul fun f g â†¦
    (dist_le (mul_nonneg C.2 dist_nonneg)).2 fun x â†¦
      calc
        dist (G (f x)) (G (g x)) â‰¤ C * dist (f x) (g x) := H.dist_le_mul _ _
        _ â‰¤ C * dist f g := by gcongr; apply dist_coe_le_dist

/-- The composition operator (in the target) with a Lipschitz map is uniformly continuous. -/
theorem uniformContinuous_comp {G : Î² â†’ Î³} {C : â„â‰¥0} (H : LipschitzWith C G) :
    UniformContinuous (comp G H : (Î± â†’áµ‡ Î²) â†’ Î± â†’áµ‡ Î³) :=
  (lipschitz_comp H).uniformContinuous

/-- The composition operator (in the target) with a Lipschitz map is continuous. -/
theorem continuous_comp {G : Î² â†’ Î³} {C : â„â‰¥0} (H : LipschitzWith C G) :
    Continuous (comp G H : (Î± â†’áµ‡ Î²) â†’ Î± â†’áµ‡ Î³) :=
  (lipschitz_comp H).continuous

/-- Restriction (in the target) of a bounded continuous function taking values in a subset. -/
def codRestrict (s : Set Î²) (f : Î± â†’áµ‡ Î²) (H : âˆ€ x, f x âˆˆ s) : Î± â†’áµ‡ s :=
  âŸ¨âŸ¨s.codRestrict f H, f.continuous.subtype_mk _âŸ©, f.boundedâŸ©

section Extend

variable {Î´ : Type*} [TopologicalSpace Î´] [DiscreteTopology Î´]

/-- A version of `Function.extend` for bounded continuous maps. We assume that the domain has
discrete topology, so we only need to verify boundedness. -/
nonrec def extend (f : Î± â†ª Î´) (g : Î± â†’áµ‡ Î²) (h : Î´ â†’áµ‡ Î²) : Î´ â†’áµ‡ Î² where
  toFun := extend f g h
  continuous_toFun := continuous_of_discreteTopology
  map_bounded' := by
    rw [â† isBounded_range_iff, range_extend f.injective]
    exact g.isBounded_range.union (h.isBounded_image _)

@[simp]
theorem extend_apply (f : Î± â†ª Î´) (g : Î± â†’áµ‡ Î²) (h : Î´ â†’áµ‡ Î²) (x : Î±) : extend f g h (f x) = g x :=
  f.injective.extend_apply _ _ _

@[simp]
nonrec theorem extend_comp (f : Î± â†ª Î´) (g : Î± â†’áµ‡ Î²) (h : Î´ â†’áµ‡ Î²) : extend f g h âˆ˜ f = g :=
  extend_comp f.injective _ _

nonrec theorem extend_apply' {f : Î± â†ª Î´} {x : Î´} (hx : x âˆ‰ range f) (g : Î± â†’áµ‡ Î²) (h : Î´ â†’áµ‡ Î²) :
    extend f g h x = h x :=
  extend_apply' _ _ _ hx

theorem extend_of_empty [IsEmpty Î±] (f : Î± â†ª Î´) (g : Î± â†’áµ‡ Î²) (h : Î´ â†’áµ‡ Î²) : extend f g h = h :=
  DFunLike.coe_injective <| Function.extend_of_isEmpty f g h

@[simp]
theorem dist_extend_extend (f : Î± â†ª Î´) (gâ‚ gâ‚‚ : Î± â†’áµ‡ Î²) (hâ‚ hâ‚‚ : Î´ â†’áµ‡ Î²) :
    dist (gâ‚.extend f hâ‚) (gâ‚‚.extend f hâ‚‚) =
      max (dist gâ‚ gâ‚‚) (dist (hâ‚.restrict (range f)á¶œ) (hâ‚‚.restrict (range f)á¶œ)) := by
  refine le_antisymm ((dist_le <| le_max_iff.2 <| Or.inl dist_nonneg).2 fun x â†¦ ?_) (max_le ?_ ?_)
  Â· rcases em (âˆƒ y, f y = x) with (âŸ¨x, rflâŸ© | hx)
    Â· simp only [extend_apply]
      exact (dist_coe_le_dist x).trans (le_max_left _ _)
    Â· simp only [extend_apply' hx]
      lift x to ((range f)á¶œ : Set Î´) using hx
      calc
        dist (hâ‚ x) (hâ‚‚ x) = dist (hâ‚.restrict (range f)á¶œ x) (hâ‚‚.restrict (range f)á¶œ x) := rfl
        _ â‰¤ dist (hâ‚.restrict (range f)á¶œ) (hâ‚‚.restrict (range f)á¶œ) := dist_coe_le_dist x
        _ â‰¤ _ := le_max_right _ _
  Â· refine (dist_le dist_nonneg).2 fun x â†¦ ?_
    rw [â† extend_apply f gâ‚ hâ‚, â† extend_apply f gâ‚‚ hâ‚‚]
    exact dist_coe_le_dist _
  Â· refine (dist_le dist_nonneg).2 fun x â†¦ ?_
    calc
      dist (hâ‚ x) (hâ‚‚ x) = dist (extend f gâ‚ hâ‚ x) (extend f gâ‚‚ hâ‚‚ x) := by
        rw [extend_apply' x.coe_prop, extend_apply' x.coe_prop]
      _ â‰¤ _ := dist_coe_le_dist _

theorem isometry_extend (f : Î± â†ª Î´) (h : Î´ â†’áµ‡ Î²) : Isometry fun g : Î± â†’áµ‡ Î² â†¦ extend f g h :=
  Isometry.of_dist_eq fun gâ‚ gâ‚‚ â†¦ by simp

end Extend

/-- The indicator function of a clopen set, as a bounded continuous function. -/
@[simps]
noncomputable def indicator (s : Set Î±) (hs : IsClopen s) : BoundedContinuousFunction Î± â„ where
  toFun := s.indicator 1
  continuous_toFun := continuous_indicator (by simp [hs]) <| continuous_const.continuousOn
  map_bounded' := âŸ¨1, fun x y â†¦ by by_cases hx : x âˆˆ s <;> by_cases hy : y âˆˆ s <;> simp [hx, hy]âŸ©

end Basics

section One

variable [TopologicalSpace Î±] [PseudoMetricSpace Î²] [One Î²]

@[to_additive] instance instOne : One (Î± â†’áµ‡ Î²) := âŸ¨const Î± 1âŸ©

@[to_additive (attr := simp)]
theorem coe_one : ((1 : Î± â†’áµ‡ Î²) : Î± â†’ Î²) = 1 := rfl

@[to_additive (attr := simp)]
theorem mkOfCompact_one [CompactSpace Î±] : mkOfCompact (1 : C(Î±, Î²)) = 1 := rfl

@[to_additive]
theorem forall_coe_one_iff_one (f : Î± â†’áµ‡ Î²) : (âˆ€ x, f x = 1) â†” f = 1 :=
  (@DFunLike.ext_iff _ _ _ _ f 1).symm

@[to_additive (attr := simp)]
theorem one_compContinuous [TopologicalSpace Î³] (f : C(Î³, Î±)) : (1 : Î± â†’áµ‡ Î²).compContinuous f = 1 :=
  rfl

end One

section mul

variable {R : Type*} [TopologicalSpace Î±] [PseudoMetricSpace R]

@[to_additive]
instance instMul [Mul R] [BoundedMul R] [ContinuousMul R] :
    Mul (Î± â†’áµ‡ R) where
  mul f g :=
    { toFun := fun x â†¦ f x * g x
      continuous_toFun := f.continuous.mul g.continuous
      map_bounded' := mul_bounded_of_bounded_of_bounded (map_bounded f) (map_bounded g) }

@[to_additive (attr := simp)]
theorem coe_mul [Mul R] [BoundedMul R] [ContinuousMul R] (f g : Î± â†’áµ‡ R) : â‡‘(f * g) = f * g := rfl

@[to_additive]
theorem mul_apply [Mul R] [BoundedMul R] [ContinuousMul R] (f g : Î± â†’áµ‡ R) (x : Î±) :
    (f * g) x = f x * g x := rfl

@[simp]
theorem coe_nsmulRec [PseudoMetricSpace Î²] [AddMonoid Î²] [BoundedAdd Î²] [ContinuousAdd Î²]
    (f : Î± â†’áµ‡ Î²) : âˆ€ n, â‡‘(nsmulRec n f) = n â€¢ â‡‘f
  | 0 => by rw [nsmulRec, zero_smul, coe_zero]
  | n + 1 => by rw [nsmulRec, succ_nsmul, coe_add, coe_nsmulRec _ n]

instance instSMulNat [PseudoMetricSpace Î²] [AddMonoid Î²] [BoundedAdd Î²] [ContinuousAdd Î²] :
    SMul â„• (Î± â†’áµ‡ Î²) where
  smul n f :=
    { toContinuousMap := n â€¢ f.toContinuousMap
      map_bounded' := by simpa [coe_nsmulRec] using (nsmulRec n f).map_bounded' }

@[to_additive existing instSMulNat]
instance instPow [Monoid R] [BoundedMul R] [ContinuousMul R] : Pow (Î± â†’áµ‡ R) â„• where
  pow f n :=
    { toFun := fun x â†¦ (f x) ^ n
      continuous_toFun := f.continuous.pow n
      map_bounded' := by
        obtain âŸ¨C, hCâŸ© := Metric.isBounded_iff.mp <| isBounded_pow (isBounded_range f) n
        exact âŸ¨C, fun x y â†¦ hC (by simp) (by simp)âŸ© }

@[to_additive]
theorem coe_pow [Monoid R] [BoundedMul R] [ContinuousMul R] (n : â„•) (f : Î± â†’áµ‡ R) :
    â‡‘(f ^ n) = (â‡‘f) ^ n := rfl

@[to_additive (attr := simp)]
theorem pow_apply [Monoid R] [BoundedMul R] [ContinuousMul R] (n : â„•) (f : Î± â†’áµ‡ R) (x : Î±) :
    (f ^ n) x = f x ^ n := rfl

@[to_additive]
instance instMonoid [Monoid R] [BoundedMul R] [ContinuousMul R] :
    Monoid (Î± â†’áµ‡ R) :=
  Injective.monoid _ DFunLike.coe_injective' rfl (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl)

@[to_additive]
instance instCommMonoid [CommMonoid R] [BoundedMul R] [ContinuousMul R] :
    CommMonoid (Î± â†’áµ‡ R) where
  __ := instMonoid
  mul_comm f g := by ext x; simp [mul_apply, mul_comm]

@[to_additive]
instance instMulOneClass [MulOneClass R] [BoundedMul R] [ContinuousMul R] : MulOneClass (Î± â†’áµ‡ R) :=
  DFunLike.coe_injective.mulOneClass _ coe_one coe_mul

/-- Composition on the left by a (lipschitz-continuous) homomorphism of topological monoids, as a
`MonoidHom`. Similar to `MonoidHom.compLeftContinuous`. -/
@[to_additive (attr := simps)
/-- Composition on the left by a (lipschitz-continuous) homomorphism of topological `AddMonoid`s,
as a `AddMonoidHom`. Similar to `AddMonoidHom.compLeftContinuous`. -/]
protected def _root_.MonoidHom.compLeftContinuousBounded (Î± : Type*)
    [TopologicalSpace Î±] [PseudoMetricSpace Î²] [Monoid Î²] [BoundedMul Î²] [ContinuousMul Î²]
    [PseudoMetricSpace Î³] [Monoid Î³] [BoundedMul Î³] [ContinuousMul Î³]
    (g : Î² â†’* Î³) {C : NNReal} (hg : LipschitzWith C g) :
    (Î± â†’áµ‡ Î²) â†’* (Î± â†’áµ‡ Î³) where
  toFun f := f.comp g hg
  map_one' := ext fun _ â†¦ g.map_one
  map_mul' _ _ := ext fun _ â†¦ g.map_mul _ _

end mul

section add

variable [TopologicalSpace Î±] [PseudoMetricSpace Î²]
variable {C : â„}

@[simp]
theorem mkOfCompact_add [CompactSpace Î±] [Add Î²] [BoundedAdd Î²] [ContinuousAdd Î²] (f g : C(Î±, Î²)) :
    mkOfCompact (f + g) = mkOfCompact f + mkOfCompact g := rfl

theorem add_compContinuous [Add Î²] [BoundedAdd Î²] [ContinuousAdd Î²] [TopologicalSpace Î³]
    (f g : Î± â†’áµ‡ Î²) (h : C(Î³, Î±)) :
    (g + f).compContinuous h = g.compContinuous h + f.compContinuous h := rfl

/-- Coercion of a `NormedAddGroupHom` is an `AddMonoidHom`. Similar to `AddMonoidHom.coeFn`. -/
@[simps]
def coeFnAddHom [AddMonoid Î²] [BoundedAdd Î²] [ContinuousAdd Î²] : (Î± â†’áµ‡ Î²) â†’+ Î± â†’ Î² where
  toFun := (â‡‘)
  map_zero' := coe_zero
  map_add' := coe_add

variable (Î± Î²)

/-- The additive map forgetting that a bounded continuous function is bounded. -/
@[simps]
def toContinuousMapAddHom [AddMonoid Î²] [BoundedAdd Î²] [ContinuousAdd Î²] : (Î± â†’áµ‡ Î²) â†’+ C(Î±, Î²) where
  toFun := toContinuousMap
  map_zero' := rfl
  map_add' := by
    intros
    ext
    simp

end add

section comm_add

variable [TopologicalSpace Î±]
variable [PseudoMetricSpace Î²] [AddCommMonoid Î²] [BoundedAdd Î²] [ContinuousAdd Î²]

@[simp]
theorem coe_sum {Î¹ : Type*} (s : Finset Î¹) (f : Î¹ â†’ Î± â†’áµ‡ Î²) :
    â‡‘(âˆ‘ i âˆˆ s, f i) = âˆ‘ i âˆˆ s, (f i : Î± â†’ Î²) :=
  map_sum coeFnAddHom f s

theorem sum_apply {Î¹ : Type*} (s : Finset Î¹) (f : Î¹ â†’ Î± â†’áµ‡ Î²) (a : Î±) :
    (âˆ‘ i âˆˆ s, f i) a = âˆ‘ i âˆˆ s, f i a := by simp

end comm_add

section LipschitzAdd

/- In this section, if `Î²` is an `AddMonoid` whose addition operation is Lipschitz, then we show
that the space of bounded continuous functions from `Î±` to `Î²` inherits a topological `AddMonoid`
structure, by using pointwise operations and checking that they are compatible with the uniform
distance.

Implementation note: The material in this section could have been written for `LipschitzMul`
and transported by `@[to_additive]`. We choose not to do this because this causes a few lemma
names (for example, `coe_mul`) to conflict with later lemma names for normed rings; this is only a
trivial inconvenience, but in any case there are no obvious applications of the multiplicative
version. -/

variable [TopologicalSpace Î±] [PseudoMetricSpace Î²] [AddMonoid Î²] [LipschitzAdd Î²]
variable (f g : Î± â†’áµ‡ Î²) {x : Î±} {C : â„}

instance instLipschitzAdd : LipschitzAdd (Î± â†’áµ‡ Î²) where
  lipschitz_add :=
    âŸ¨LipschitzAdd.C Î², by
      have C_nonneg := (LipschitzAdd.C Î²).coe_nonneg
      rw [lipschitzWith_iff_dist_le_mul]
      rintro âŸ¨fâ‚, gâ‚âŸ© âŸ¨fâ‚‚, gâ‚‚âŸ©
      rw [dist_le (mul_nonneg C_nonneg dist_nonneg)]
      intro x
      refine le_trans (lipschitz_with_lipschitz_const_add âŸ¨fâ‚ x, gâ‚ xâŸ© âŸ¨fâ‚‚ x, gâ‚‚ xâŸ©) ?_
      gcongr
      apply max_le_max <;> exact dist_coe_le_dist xâŸ©

end LipschitzAdd

section sub

variable [TopologicalSpace Î±]
variable {R : Type*} [PseudoMetricSpace R] [Sub R] [BoundedSub R] [ContinuousSub R]
variable (f g : Î± â†’áµ‡ R)

/-- The pointwise difference of two bounded continuous functions is again bounded continuous. -/
instance instSub : Sub (Î± â†’áµ‡ R) where
  sub f g :=
    { toFun := fun x â†¦ (f x - g x),
      map_bounded' := sub_bounded_of_bounded_of_bounded f.map_bounded' g.map_bounded' }

theorem sub_apply {x : Î±} : (f - g) x = f x - g x := rfl

@[simp]
theorem coe_sub : â‡‘(f - g) = f - g := rfl

end sub

section casts

variable [TopologicalSpace Î±] {Î² : Type*} [PseudoMetricSpace Î²]

instance [NatCast Î²] : NatCast (Î± â†’áµ‡ Î²) := âŸ¨fun n â†¦ BoundedContinuousFunction.const _ nâŸ©

@[simp]
theorem natCast_apply [NatCast Î²] (n : â„•) (x : Î±) : (n : Î± â†’áµ‡ Î²) x = n := rfl

instance [IntCast Î²] : IntCast (Î± â†’áµ‡ Î²) := âŸ¨fun m â†¦ BoundedContinuousFunction.const _ mâŸ©

@[simp]
theorem intCast_apply [IntCast Î²] (m : â„¤) (x : Î±) : (m : Î± â†’áµ‡ Î²) x = m := rfl

end casts

instance instSemiring {R : Type*} [TopologicalSpace Î±] [PseudoMetricSpace R]
    [Semiring R] [BoundedMul R] [ContinuousMul R] [BoundedAdd R] [ContinuousAdd R] :
    Semiring (Î± â†’áµ‡ R) :=
  Injective.semiring _ DFunLike.coe_injective'
    rfl rfl (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl) (fun _ â†¦ rfl)

section IsBoundedSMul

/-!
### `IsBoundedSMul` (in particular, topological module) structure

In this section, if `Î²` is a metric space and a `ğ•œ`-module whose addition and scalar multiplication
are compatible with the metric structure, then we show that the space of bounded continuous
functions from `Î±` to `Î²` inherits a so-called `IsBoundedSMul` structure (in particular, a
`ContinuousMul` structure, which is the mathlib formulation of being a topological module), by
using pointwise operations and checking that they are compatible with the uniform distance. -/


variable {ğ•œ : Type*} [PseudoMetricSpace ğ•œ] [TopologicalSpace Î±] [PseudoMetricSpace Î²]

section SMul

variable [Zero ğ•œ] [Zero Î²] [SMul ğ•œ Î²] [IsBoundedSMul ğ•œ Î²]

instance instSMul : SMul ğ•œ (Î± â†’áµ‡ Î²) where
  smul c f :=
    { toContinuousMap := c â€¢ f.toContinuousMap
      map_bounded' :=
        let âŸ¨b, hbâŸ© := f.bounded
        âŸ¨dist c 0 * b, fun x y â†¦ by
          refine (dist_smul_pair c (f x) (f y)).trans ?_
          gcongr
          apply hbâŸ© }

@[simp]
theorem coe_smul (c : ğ•œ) (f : Î± â†’áµ‡ Î²) : â‡‘(c â€¢ f) = fun x â†¦ c â€¢ f x := rfl

theorem smul_apply (c : ğ•œ) (f : Î± â†’áµ‡ Î²) (x : Î±) : (c â€¢ f) x = c â€¢ f x := rfl

instance instIsScalarTower {ğ•œ' : Type*} [PseudoMetricSpace ğ•œ'] [Zero ğ•œ'] [SMul ğ•œ' Î²]
    [IsBoundedSMul ğ•œ' Î²] [SMul ğ•œ' ğ•œ] [IsScalarTower ğ•œ' ğ•œ Î²] :
    IsScalarTower ğ•œ' ğ•œ (Î± â†’áµ‡ Î²) where
  smul_assoc _ _ _ := ext fun _ â†¦ smul_assoc ..

instance instSMulCommClass {ğ•œ' : Type*} [PseudoMetricSpace ğ•œ'] [Zero ğ•œ'] [SMul ğ•œ' Î²]
    [IsBoundedSMul ğ•œ' Î²] [SMulCommClass ğ•œ' ğ•œ Î²] :
    SMulCommClass ğ•œ' ğ•œ (Î± â†’áµ‡ Î²) where
  smul_comm _ _ _ := ext fun _ â†¦ smul_comm ..

instance instIsCentralScalar [SMul ğ•œáµáµ’áµ– Î²] [IsCentralScalar ğ•œ Î²] : IsCentralScalar ğ•œ (Î± â†’áµ‡ Î²) where
  op_smul_eq_smul _ _ := ext fun _ â†¦ op_smul_eq_smul _ _

instance instIsBoundedSMul : IsBoundedSMul ğ•œ (Î± â†’áµ‡ Î²) where
  dist_smul_pair' c fâ‚ fâ‚‚ := by
    rw [dist_le (mul_nonneg dist_nonneg dist_nonneg)]
    intro x
    refine (dist_smul_pair c (fâ‚ x) (fâ‚‚ x)).trans ?_
    gcongr
    apply dist_coe_le_dist
  dist_pair_smul' câ‚ câ‚‚ f := by
    rw [dist_le (by positivity)]
    intro x
    refine (dist_pair_smul câ‚ câ‚‚ (f x)).trans ?_
    gcongr
    apply dist_coe_le_dist (g := 0)

end SMul

section MulAction

variable [MonoidWithZero ğ•œ] [Zero Î²] [MulAction ğ•œ Î²] [IsBoundedSMul ğ•œ Î²]

instance instMulAction : MulAction ğ•œ (Î± â†’áµ‡ Î²) :=
  DFunLike.coe_injective.mulAction _ coe_smul

end MulAction

section DistribMulAction

variable [MonoidWithZero ğ•œ] [AddMonoid Î²] [DistribMulAction ğ•œ Î²] [IsBoundedSMul ğ•œ Î²]
variable [BoundedAdd Î²] [ContinuousAdd Î²]

instance instDistribMulAction : DistribMulAction ğ•œ (Î± â†’áµ‡ Î²) :=
  DFunLike.coe_injective.distribMulAction âŸ¨âŸ¨_, coe_zeroâŸ©, coe_addâŸ© coe_smul

end DistribMulAction

section Module

variable [Semiring ğ•œ] [AddCommMonoid Î²] [Module ğ•œ Î²] [IsBoundedSMul ğ•œ Î²]
variable {f g : Î± â†’áµ‡ Î²} {x : Î±} {C : â„}
variable [BoundedAdd Î²] [ContinuousAdd Î²]

instance instModule : Module ğ•œ (Î± â†’áµ‡ Î²) :=
  DFunLike.coe_injective.module _ âŸ¨âŸ¨_, coe_zeroâŸ©, coe_addâŸ©  coe_smul

variable (ğ•œ)

/-- The evaluation at a point, as a continuous linear map from `Î± â†’áµ‡ Î²` to `Î²`. -/
@[simps]
def evalCLM (x : Î±) : (Î± â†’áµ‡ Î²) â†’L[ğ•œ] Î² where
  toFun f := f x
  map_add' _ _ := add_apply _ _ _
  map_smul' _ _ := smul_apply _ _ _

variable (Î± Î²)

/-- The linear map forgetting that a bounded continuous function is bounded. -/
@[simps]
def toContinuousMapLinearMap : (Î± â†’áµ‡ Î²) â†’â‚—[ğ•œ] C(Î±, Î²) where
  toFun := toContinuousMap
  map_smul' _ _ := rfl
  map_add' _ _ := rfl

end Module

end IsBoundedSMul

theorem NNReal.upper_bound {Î± : Type*} [TopologicalSpace Î±] (f : Î± â†’áµ‡ â„â‰¥0) (x : Î±) :
    f x â‰¤ nndist f 0 := by
  have key : nndist (f x) ((0 : Î± â†’áµ‡ â„â‰¥0) x) â‰¤ nndist f 0 := @dist_coe_le_dist Î± â„â‰¥0 _ _ f 0 x
  simp only [coe_zero, Pi.zero_apply] at key
  rwa [NNReal.nndist_zero_eq_val' (f x)] at key

end BoundedContinuousFunction
