/-
Copyright (c) 2018 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Mario Carneiro, Yury Kudryashov, Heather Macbeth
-/
import Mathlib.Topology.Algebra.Indicator
import Mathlib.Topology.Bornology.BoundedOperation
import Mathlib.Topology.ContinuousMap.Algebra

/-!
# Bounded continuous functions

The type of bounded continuous functions taking values in a metric space, with the uniform distance.
-/

assert_not_exists CStarRing

noncomputable section

open Topology Bornology NNReal UniformConvergence

open Set Filter Metric Function

universe u v w

variable {F : Type*} {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w}

/-- `Œ± ‚Üí·µá Œ≤` is the type of bounded continuous functions `Œ± ‚Üí Œ≤` from a topological space to a
metric space.

When possible, instead of parametrizing results over `(f : Œ± ‚Üí·µá Œ≤)`,
you should parametrize over `(F : Type*) [BoundedContinuousMapClass F Œ± Œ≤] (f : F)`.

When you extend this structure, make sure to extend `BoundedContinuousMapClass`. -/
structure BoundedContinuousFunction (Œ± : Type u) (Œ≤ : Type v) [TopologicalSpace Œ±]
    [PseudoMetricSpace Œ≤] : Type max u v extends ContinuousMap Œ± Œ≤ where
  map_bounded' : ‚àÉ C, ‚àÄ x y, dist (toFun x) (toFun y) ‚â§ C

@[inherit_doc] scoped[BoundedContinuousFunction] infixr:25 " ‚Üí·µá " => BoundedContinuousFunction

section

/-- `BoundedContinuousMapClass F Œ± Œ≤` states that `F` is a type of bounded continuous maps.

You should also extend this typeclass when you extend `BoundedContinuousFunction`. -/
class BoundedContinuousMapClass (F : Type*) (Œ± Œ≤ : outParam Type*) [TopologicalSpace Œ±]
    [PseudoMetricSpace Œ≤] [FunLike F Œ± Œ≤] : Prop extends ContinuousMapClass F Œ± Œ≤ where
  map_bounded (f : F) : ‚àÉ C, ‚àÄ x y, dist (f x) (f y) ‚â§ C

end

export BoundedContinuousMapClass (map_bounded)

namespace BoundedContinuousFunction

section Basics

variable [TopologicalSpace Œ±] [PseudoMetricSpace Œ≤] [PseudoMetricSpace Œ≥]
variable {f g : Œ± ‚Üí·µá Œ≤} {x : Œ±} {C : ‚Ñù}

instance instFunLike : FunLike (Œ± ‚Üí·µá Œ≤) Œ± Œ≤ where
  coe f := f.toFun
  coe_injective' f g h := by
    obtain ‚ü®‚ü®_, _‚ü©, _‚ü© := f
    obtain ‚ü®‚ü®_, _‚ü©, _‚ü© := g
    congr

instance instBoundedContinuousMapClass : BoundedContinuousMapClass (Œ± ‚Üí·µá Œ≤) Œ± Œ≤ where
  map_continuous f := f.continuous_toFun
  map_bounded f := f.map_bounded'

instance instCoeTC [FunLike F Œ± Œ≤] [BoundedContinuousMapClass F Œ± Œ≤] : CoeTC F (Œ± ‚Üí·µá Œ≤) :=
  ‚ü®fun f =>
    { toFun := f
      continuous_toFun := map_continuous f
      map_bounded' := map_bounded f }‚ü©

@[simp]
theorem coe_toContinuousMap (f : Œ± ‚Üí·µá Œ≤) : (f.toContinuousMap : Œ± ‚Üí Œ≤) = f := rfl

/-- See Note [custom simps projection]. We need to specify this projection explicitly in this case,
  because it is a composition of multiple projections. -/
def Simps.apply (h : Œ± ‚Üí·µá Œ≤) : Œ± ‚Üí Œ≤ := h

initialize_simps_projections BoundedContinuousFunction (toFun ‚Üí apply)

protected theorem bounded (f : Œ± ‚Üí·µá Œ≤) : ‚àÉ C, ‚àÄ x y : Œ±, dist (f x) (f y) ‚â§ C :=
  f.map_bounded'

protected theorem continuous (f : Œ± ‚Üí·µá Œ≤) : Continuous f :=
  f.toContinuousMap.continuous

@[ext]
theorem ext (h : ‚àÄ x, f x = g x) : f = g :=
  DFunLike.ext _ _ h

theorem isBounded_range (f : Œ± ‚Üí·µá Œ≤) : IsBounded (range f) :=
  isBounded_range_iff.2 f.bounded

theorem isBounded_image (f : Œ± ‚Üí·µá Œ≤) (s : Set Œ±) : IsBounded (f '' s) :=
  f.isBounded_range.subset <| image_subset_range _ _

theorem eq_of_empty [h : IsEmpty Œ±] (f g : Œ± ‚Üí·µá Œ≤) : f = g :=
  ext <| h.elim

/-- A continuous function with an explicit bound is a bounded continuous function. -/
def mkOfBound (f : C(Œ±, Œ≤)) (C : ‚Ñù) (h : ‚àÄ x y : Œ±, dist (f x) (f y) ‚â§ C) : Œ± ‚Üí·µá Œ≤ :=
  ‚ü®f, ‚ü®C, h‚ü©‚ü©

@[simp]
theorem mkOfBound_coe {f} {C} {h} : (mkOfBound f C h : Œ± ‚Üí Œ≤) = (f : Œ± ‚Üí Œ≤) := rfl

/-- A continuous function on a compact space is automatically a bounded continuous function. -/
def mkOfCompact [CompactSpace Œ±] (f : C(Œ±, Œ≤)) : Œ± ‚Üí·µá Œ≤ :=
  ‚ü®f, isBounded_range_iff.1 (isCompact_range f.continuous).isBounded‚ü©

@[simp]
theorem mkOfCompact_apply [CompactSpace Œ±] (f : C(Œ±, Œ≤)) (a : Œ±) : mkOfCompact f a = f a := rfl

/-- If a function is bounded on a discrete space, it is automatically continuous,
and therefore gives rise to an element of the type of bounded continuous functions. -/
@[simps]
def mkOfDiscrete [DiscreteTopology Œ±] (f : Œ± ‚Üí Œ≤) (C : ‚Ñù) (h : ‚àÄ x y : Œ±, dist (f x) (f y) ‚â§ C) :
    Œ± ‚Üí·µá Œ≤ :=
  ‚ü®‚ü®f, continuous_of_discreteTopology‚ü©, ‚ü®C, h‚ü©‚ü©

/-- The uniform distance between two bounded continuous functions. -/
instance instDist : Dist (Œ± ‚Üí·µá Œ≤) :=
  ‚ü®fun f g => sInf { C | 0 ‚â§ C ‚àß ‚àÄ x : Œ±, dist (f x) (g x) ‚â§ C }‚ü©

theorem dist_eq : dist f g = sInf { C | 0 ‚â§ C ‚àß ‚àÄ x : Œ±, dist (f x) (g x) ‚â§ C } := rfl

theorem dist_set_exists : ‚àÉ C, 0 ‚â§ C ‚àß ‚àÄ x : Œ±, dist (f x) (g x) ‚â§ C := by
  rcases isBounded_iff.1 (f.isBounded_range.union g.isBounded_range) with ‚ü®C, hC‚ü©
  refine ‚ü®max 0 C, le_max_left _ _, fun x => (hC ?_ ?_).trans (le_max_right _ _)‚ü©
    <;> [left; right]
    <;> apply mem_range_self

/-- The pointwise distance is controlled by the distance between functions, by definition. -/
theorem dist_coe_le_dist (x : Œ±) : dist (f x) (g x) ‚â§ dist f g :=
  le_csInf dist_set_exists fun _ hb => hb.2 x

/- This lemma will be needed in the proof of the metric space instance, but it will become
useless afterwards as it will be superseded by the general result that the distance is nonnegative
in metric spaces. -/
private theorem dist_nonneg' : 0 ‚â§ dist f g :=
  le_csInf dist_set_exists fun _ => And.left

/-- The distance between two functions is controlled by the supremum of the pointwise distances. -/
theorem dist_le (C0 : (0 : ‚Ñù) ‚â§ C) : dist f g ‚â§ C ‚Üî ‚àÄ x : Œ±, dist (f x) (g x) ‚â§ C :=
  ‚ü®fun h x => le_trans (dist_coe_le_dist x) h, fun H => csInf_le ‚ü®0, fun _ => And.left‚ü© ‚ü®C0, H‚ü©‚ü©

theorem dist_le_iff_of_nonempty [Nonempty Œ±] : dist f g ‚â§ C ‚Üî ‚àÄ x, dist (f x) (g x) ‚â§ C :=
  ‚ü®fun h x => le_trans (dist_coe_le_dist x) h,
    fun w => (dist_le (le_trans dist_nonneg (w (Nonempty.some ‚Äπ_‚Ä∫)))).mpr w‚ü©

theorem dist_lt_of_nonempty_compact [Nonempty Œ±] [CompactSpace Œ±]
    (w : ‚àÄ x : Œ±, dist (f x) (g x) < C) : dist f g < C := by
  have c : Continuous fun x => dist (f x) (g x) := by fun_prop
  obtain ‚ü®x, -, le‚ü© :=
    IsCompact.exists_isMaxOn isCompact_univ Set.univ_nonempty (Continuous.continuousOn c)
  exact lt_of_le_of_lt (dist_le_iff_of_nonempty.mpr fun y => le trivial) (w x)

theorem dist_lt_iff_of_compact [CompactSpace Œ±] (C0 : (0 : ‚Ñù) < C) :
    dist f g < C ‚Üî ‚àÄ x : Œ±, dist (f x) (g x) < C := by
  fconstructor
  ¬∑ intro w x
    exact lt_of_le_of_lt (dist_coe_le_dist x) w
  ¬∑ by_cases h : Nonempty Œ±
    ¬∑ exact dist_lt_of_nonempty_compact
    ¬∑ rintro -
      convert C0
      apply le_antisymm _ dist_nonneg'
      rw [dist_eq]
      exact csInf_le ‚ü®0, fun C => And.left‚ü© ‚ü®le_rfl, fun x => False.elim (h (Nonempty.intro x))‚ü©

theorem dist_lt_iff_of_nonempty_compact [Nonempty Œ±] [CompactSpace Œ±] :
    dist f g < C ‚Üî ‚àÄ x : Œ±, dist (f x) (g x) < C :=
  ‚ü®fun w x => lt_of_le_of_lt (dist_coe_le_dist x) w, dist_lt_of_nonempty_compact‚ü©

/-- The type of bounded continuous functions, with the uniform distance, is a pseudometric space. -/
instance instPseudoMetricSpace : PseudoMetricSpace (Œ± ‚Üí·µá Œ≤) where
  dist_self f := le_antisymm ((dist_le le_rfl).2 fun x => by simp) dist_nonneg'
  dist_comm f g := by simp [dist_eq, dist_comm]
  dist_triangle _ _ _ := (dist_le (add_nonneg dist_nonneg' dist_nonneg')).2
    fun _ => le_trans (dist_triangle _ _ _) (add_le_add (dist_coe_le_dist _) (dist_coe_le_dist _))

/-- The type of bounded continuous functions, with the uniform distance, is a metric space. -/
instance instMetricSpace {Œ≤} [MetricSpace Œ≤] : MetricSpace (Œ± ‚Üí·µá Œ≤) where
  eq_of_dist_eq_zero hfg := by
    ext x
    exact eq_of_dist_eq_zero (le_antisymm (hfg ‚ñ∏ dist_coe_le_dist _) dist_nonneg)

theorem nndist_eq : nndist f g = sInf { C | ‚àÄ x : Œ±, nndist (f x) (g x) ‚â§ C } :=
  Subtype.ext <| dist_eq.trans <| by
    rw [val_eq_coe, coe_sInf, coe_image]
    simp_rw [mem_setOf_eq, ‚Üê NNReal.coe_le_coe, coe_mk, exists_prop, coe_nndist]

theorem nndist_set_exists : ‚àÉ C, ‚àÄ x : Œ±, nndist (f x) (g x) ‚â§ C :=
  Subtype.exists.mpr <| dist_set_exists.imp fun _ ‚ü®ha, h‚ü© => ‚ü®ha, h‚ü©

theorem nndist_coe_le_nndist (x : Œ±) : nndist (f x) (g x) ‚â§ nndist f g :=
  dist_coe_le_dist x

/-- On an empty space, bounded continuous functions are at distance 0. -/
theorem dist_zero_of_empty [IsEmpty Œ±] : dist f g = 0 := by
  rw [(ext isEmptyElim : f = g), dist_self]

theorem dist_eq_iSup : dist f g = ‚®Ü x : Œ±, dist (f x) (g x) := by
  cases isEmpty_or_nonempty Œ±
  ¬∑ rw [iSup_of_empty', Real.sSup_empty, dist_zero_of_empty]
  refine (dist_le_iff_of_nonempty.mpr <| le_ciSup ?_).antisymm (ciSup_le dist_coe_le_dist)
  exact dist_set_exists.imp fun C hC => forall_mem_range.2 hC.2

theorem nndist_eq_iSup : nndist f g = ‚®Ü x : Œ±, nndist (f x) (g x) :=
  Subtype.ext <| dist_eq_iSup.trans <| by simp_rw [val_eq_coe, coe_iSup, coe_nndist]

theorem edist_eq_iSup : edist f g = ‚®Ü x, edist (f x) (g x) := by
  simp_rw [edist_nndist, nndist_eq_iSup]
  refine ENNReal.coe_iSup ‚ü®nndist f g, ?_‚ü©
  rintro - ‚ü®x, hx, rfl‚ü©
  exact nndist_coe_le_nndist x

theorem tendsto_iff_tendstoUniformly {Œπ : Type*} {F : Œπ ‚Üí Œ± ‚Üí·µá Œ≤} {f : Œ± ‚Üí·µá Œ≤} {l : Filter Œπ} :
    Tendsto F l (ùìù f) ‚Üî TendstoUniformly (fun i => F i) f l :=
  Iff.intro
    (fun h =>
      tendstoUniformly_iff.2 fun Œµ Œµ0 =>
        (Metric.tendsto_nhds.mp h Œµ Œµ0).mp
          (Eventually.of_forall fun n hn x =>
            lt_of_le_of_lt (dist_coe_le_dist x) (dist_comm (F n) f ‚ñ∏ hn)))
    fun h =>
    Metric.tendsto_nhds.mpr fun _ Œµ_pos =>
      (h _ (dist_mem_uniformity <| half_pos Œµ_pos)).mp
        (Eventually.of_forall fun n hn =>
          lt_of_le_of_lt
            ((dist_le (half_pos Œµ_pos).le).mpr fun x => dist_comm (f x) (F n x) ‚ñ∏ le_of_lt (hn x))
            (half_lt_self Œµ_pos))

/-- The topology on `Œ± ‚Üí·µá Œ≤` is exactly the topology induced by the natural map to `Œ± ‚Üí·µ§ Œ≤`. -/
theorem isInducing_coeFn : IsInducing (UniformFun.ofFun ‚àò (‚áë) : (Œ± ‚Üí·µá Œ≤) ‚Üí Œ± ‚Üí·µ§ Œ≤) := by
  rw [isInducing_iff_nhds]
  refine fun f => eq_of_forall_le_iff fun l => ?_
  rw [‚Üê tendsto_iff_comap, ‚Üê tendsto_id', tendsto_iff_tendstoUniformly,
    UniformFun.tendsto_iff_tendstoUniformly]
  simp [comp_def]

-- TODO: upgrade to `IsUniformEmbedding`
theorem isEmbedding_coeFn : IsEmbedding (UniformFun.ofFun ‚àò (‚áë) : (Œ± ‚Üí·µá Œ≤) ‚Üí Œ± ‚Üí·µ§ Œ≤) :=
  ‚ü®isInducing_coeFn, fun _ _ h => ext fun x => congr_fun h x‚ü©

variable (Œ±) in
/-- Constant as a continuous bounded function. -/
@[simps! -fullyApplied]
def const (b : Œ≤) : Œ± ‚Üí·µá Œ≤ :=
  ‚ü®ContinuousMap.const Œ± b, 0, by simp‚ü©

theorem const_apply' (a : Œ±) (b : Œ≤) : (const Œ± b : Œ± ‚Üí Œ≤) a = b := rfl

/-- If the target space is inhabited, so is the space of bounded continuous functions. -/
instance [Inhabited Œ≤] : Inhabited (Œ± ‚Üí·µá Œ≤) :=
  ‚ü®const Œ± default‚ü©

theorem lipschitz_evalx (x : Œ±) : LipschitzWith 1 fun f : Œ± ‚Üí·µá Œ≤ => f x :=
  LipschitzWith.mk_one fun _ _ => dist_coe_le_dist x

theorem uniformContinuous_coe : @UniformContinuous (Œ± ‚Üí·µá Œ≤) (Œ± ‚Üí Œ≤) _ _ (‚áë) :=
  uniformContinuous_pi.2 fun x => (lipschitz_evalx x).uniformContinuous

theorem continuous_coe : Continuous fun (f : Œ± ‚Üí·µá Œ≤) x => f x :=
  UniformContinuous.continuous uniformContinuous_coe

/-- When `x` is fixed, `(f : Œ± ‚Üí·µá Œ≤) ‚Ü¶ f x` is continuous. -/
@[continuity]
theorem continuous_eval_const {x : Œ±} : Continuous fun f : Œ± ‚Üí·µá Œ≤ => f x :=
  (continuous_apply x).comp continuous_coe

/-- The evaluation map is continuous, as a joint function of `u` and `x`. -/
@[continuity]
theorem continuous_eval : Continuous fun p : (Œ± ‚Üí·µá Œ≤) √ó Œ± => p.1 p.2 :=
  (continuous_prod_of_continuous_lipschitzWith _ 1 fun f => f.continuous) <| lipschitz_evalx

/-- Bounded continuous functions taking values in a complete space form a complete space. -/
instance instCompleteSpace [CompleteSpace Œ≤] : CompleteSpace (Œ± ‚Üí·µá Œ≤) :=
  complete_of_cauchySeq_tendsto fun (f : ‚Ñï ‚Üí Œ± ‚Üí·µá Œ≤) (hf : CauchySeq f) => by
    /- We have to show that `f n` converges to a bounded continuous function.
      For this, we prove pointwise convergence to define the limit, then check
      it is a continuous bounded function, and then check the norm convergence. -/
    rcases cauchySeq_iff_le_tendsto_0.1 hf with ‚ü®b, b0, b_bound, b_lim‚ü©
    have f_bdd := fun x n m N hn hm => le_trans (dist_coe_le_dist x) (b_bound n m N hn hm)
    have fx_cau : ‚àÄ x, CauchySeq fun n => f n x :=
      fun x => cauchySeq_iff_le_tendsto_0.2 ‚ü®b, b0, f_bdd x, b_lim‚ü©
    choose F hF using fun x => cauchySeq_tendsto_of_complete (fx_cau x)
    /- `F : Œ± ‚Üí Œ≤`, `hF : ‚àÄ (x : Œ±), Tendsto (fun n ‚Ü¶ ‚Üë(f n) x) atTop (ùìù (F x))`
      `F` is the desired limit function. Check that it is uniformly approximated by `f N`. -/
    have fF_bdd : ‚àÄ x N, dist (f N x) (F x) ‚â§ b N :=
      fun x N => le_of_tendsto (tendsto_const_nhds.dist (hF x))
        (Filter.eventually_atTop.2 ‚ü®N, fun n hn => f_bdd x N n N (le_refl N) hn‚ü©)
    refine ‚ü®‚ü®‚ü®F, ?_‚ü©, ?_‚ü©, ?_‚ü©
    ¬∑ -- Check that `F` is continuous, as a uniform limit of continuous functions
      have : TendstoUniformly (fun n x => f n x) F atTop := by
        refine Metric.tendstoUniformly_iff.2 fun Œµ Œµ0 => ?_
        refine ((tendsto_order.1 b_lim).2 Œµ Œµ0).mono fun n hn x => ?_
        rw [dist_comm]
        exact lt_of_le_of_lt (fF_bdd x n) hn
      exact this.continuous (Eventually.of_forall fun N => (f N).continuous)
    ¬∑ -- Check that `F` is bounded
      rcases (f 0).bounded with ‚ü®C, hC‚ü©
      refine ‚ü®C + (b 0 + b 0), fun x y => ?_‚ü©
      calc
        dist (F x) (F y) ‚â§ dist (f 0 x) (f 0 y) + (dist (f 0 x) (F x) + dist (f 0 y) (F y)) :=
          dist_triangle4_left _ _ _ _
        _ ‚â§ C + (b 0 + b 0) := add_le_add (hC x y) (add_le_add (fF_bdd x 0) (fF_bdd y 0))
    ¬∑ -- Check that `F` is close to `f N` in distance terms
      refine tendsto_iff_dist_tendsto_zero.2 (squeeze_zero (fun _ => dist_nonneg) ?_ b_lim)
      exact fun N => (dist_le (b0 _)).2 fun x => fF_bdd x N

/-- Composition of a bounded continuous function and a continuous function. -/
def compContinuous {Œ¥ : Type*} [TopologicalSpace Œ¥] (f : Œ± ‚Üí·µá Œ≤) (g : C(Œ¥, Œ±)) : Œ¥ ‚Üí·µá Œ≤ where
  toContinuousMap := f.1.comp g
  map_bounded' := f.map_bounded'.imp fun _ hC _ _ => hC _ _

@[simp]
theorem coe_compContinuous {Œ¥ : Type*} [TopologicalSpace Œ¥] (f : Œ± ‚Üí·µá Œ≤) (g : C(Œ¥, Œ±)) :
    ‚áë(f.compContinuous g) = f ‚àò g := rfl

@[simp]
theorem compContinuous_apply {Œ¥ : Type*} [TopologicalSpace Œ¥] (f : Œ± ‚Üí·µá Œ≤) (g : C(Œ¥, Œ±)) (x : Œ¥) :
    f.compContinuous g x = f (g x) := rfl

theorem lipschitz_compContinuous {Œ¥ : Type*} [TopologicalSpace Œ¥] (g : C(Œ¥, Œ±)) :
    LipschitzWith 1 fun f : Œ± ‚Üí·µá Œ≤ => f.compContinuous g :=
  LipschitzWith.mk_one fun _ _ => (dist_le dist_nonneg).2 fun x => dist_coe_le_dist (g x)

theorem continuous_compContinuous {Œ¥ : Type*} [TopologicalSpace Œ¥] (g : C(Œ¥, Œ±)) :
    Continuous fun f : Œ± ‚Üí·µá Œ≤ => f.compContinuous g :=
  (lipschitz_compContinuous g).continuous

/-- Restrict a bounded continuous function to a set. -/
def restrict (f : Œ± ‚Üí·µá Œ≤) (s : Set Œ±) : s ‚Üí·µá Œ≤ :=
  f.compContinuous <| (ContinuousMap.id _).restrict s

@[simp]
theorem coe_restrict (f : Œ± ‚Üí·µá Œ≤) (s : Set Œ±) : ‚áë(f.restrict s) = f ‚àò (‚Üë) := rfl

@[simp]
theorem restrict_apply (f : Œ± ‚Üí·µá Œ≤) (s : Set Œ±) (x : s) : f.restrict s x = f x := rfl

/-- Composition (in the target) of a bounded continuous function with a Lipschitz map again
gives a bounded continuous function. -/
def comp (G : Œ≤ ‚Üí Œ≥) {C : ‚Ñù‚â•0} (H : LipschitzWith C G) (f : Œ± ‚Üí·µá Œ≤) : Œ± ‚Üí·µá Œ≥ :=
  ‚ü®‚ü®fun x => G (f x), H.continuous.comp f.continuous‚ü©,
    let ‚ü®D, hD‚ü© := f.bounded
    ‚ü®max C 0 * D, fun x y =>
      calc
        dist (G (f x)) (G (f y)) ‚â§ C * dist (f x) (f y) := H.dist_le_mul _ _
        _ ‚â§ max C 0 * dist (f x) (f y) := by gcongr; apply le_max_left
        _ ‚â§ max C 0 * D := by gcongr; apply hD
        ‚ü©‚ü©

@[simp]
theorem comp_apply (G : Œ≤ ‚Üí Œ≥) {C : ‚Ñù‚â•0} (H : LipschitzWith C G) (f : Œ± ‚Üí·µá Œ≤) (a : Œ±) :
    (f.comp G H) a = G (f a) := rfl

/-- The composition operator (in the target) with a Lipschitz map is Lipschitz. -/
theorem lipschitz_comp {G : Œ≤ ‚Üí Œ≥} {C : ‚Ñù‚â•0} (H : LipschitzWith C G) :
    LipschitzWith C (comp G H : (Œ± ‚Üí·µá Œ≤) ‚Üí Œ± ‚Üí·µá Œ≥) :=
  LipschitzWith.of_dist_le_mul fun f g =>
    (dist_le (mul_nonneg C.2 dist_nonneg)).2 fun x =>
      calc
        dist (G (f x)) (G (g x)) ‚â§ C * dist (f x) (g x) := H.dist_le_mul _ _
        _ ‚â§ C * dist f g := by gcongr; apply dist_coe_le_dist

/-- The composition operator (in the target) with a Lipschitz map is uniformly continuous. -/
theorem uniformContinuous_comp {G : Œ≤ ‚Üí Œ≥} {C : ‚Ñù‚â•0} (H : LipschitzWith C G) :
    UniformContinuous (comp G H : (Œ± ‚Üí·µá Œ≤) ‚Üí Œ± ‚Üí·µá Œ≥) :=
  (lipschitz_comp H).uniformContinuous

/-- The composition operator (in the target) with a Lipschitz map is continuous. -/
theorem continuous_comp {G : Œ≤ ‚Üí Œ≥} {C : ‚Ñù‚â•0} (H : LipschitzWith C G) :
    Continuous (comp G H : (Œ± ‚Üí·µá Œ≤) ‚Üí Œ± ‚Üí·µá Œ≥) :=
  (lipschitz_comp H).continuous

/-- Restriction (in the target) of a bounded continuous function taking values in a subset. -/
def codRestrict (s : Set Œ≤) (f : Œ± ‚Üí·µá Œ≤) (H : ‚àÄ x, f x ‚àà s) : Œ± ‚Üí·µá s :=
  ‚ü®‚ü®s.codRestrict f H, f.continuous.subtype_mk _‚ü©, f.bounded‚ü©

section Extend

variable {Œ¥ : Type*} [TopologicalSpace Œ¥] [DiscreteTopology Œ¥]

/-- A version of `Function.extend` for bounded continuous maps. We assume that the domain has
discrete topology, so we only need to verify boundedness. -/
nonrec def extend (f : Œ± ‚Ü™ Œ¥) (g : Œ± ‚Üí·µá Œ≤) (h : Œ¥ ‚Üí·µá Œ≤) : Œ¥ ‚Üí·µá Œ≤ where
  toFun := extend f g h
  continuous_toFun := continuous_of_discreteTopology
  map_bounded' := by
    rw [‚Üê isBounded_range_iff, range_extend f.injective]
    exact g.isBounded_range.union (h.isBounded_image _)

@[simp]
theorem extend_apply (f : Œ± ‚Ü™ Œ¥) (g : Œ± ‚Üí·µá Œ≤) (h : Œ¥ ‚Üí·µá Œ≤) (x : Œ±) : extend f g h (f x) = g x :=
  f.injective.extend_apply _ _ _

@[simp]
nonrec theorem extend_comp (f : Œ± ‚Ü™ Œ¥) (g : Œ± ‚Üí·µá Œ≤) (h : Œ¥ ‚Üí·µá Œ≤) : extend f g h ‚àò f = g :=
  extend_comp f.injective _ _

nonrec theorem extend_apply' {f : Œ± ‚Ü™ Œ¥} {x : Œ¥} (hx : x ‚àâ range f) (g : Œ± ‚Üí·µá Œ≤) (h : Œ¥ ‚Üí·µá Œ≤) :
    extend f g h x = h x :=
  extend_apply' _ _ _ hx

theorem extend_of_empty [IsEmpty Œ±] (f : Œ± ‚Ü™ Œ¥) (g : Œ± ‚Üí·µá Œ≤) (h : Œ¥ ‚Üí·µá Œ≤) : extend f g h = h :=
  DFunLike.coe_injective <| Function.extend_of_isEmpty f g h

@[simp]
theorem dist_extend_extend (f : Œ± ‚Ü™ Œ¥) (g‚ÇÅ g‚ÇÇ : Œ± ‚Üí·µá Œ≤) (h‚ÇÅ h‚ÇÇ : Œ¥ ‚Üí·µá Œ≤) :
    dist (g‚ÇÅ.extend f h‚ÇÅ) (g‚ÇÇ.extend f h‚ÇÇ) =
      max (dist g‚ÇÅ g‚ÇÇ) (dist (h‚ÇÅ.restrict (range f)·∂ú) (h‚ÇÇ.restrict (range f)·∂ú)) := by
  refine le_antisymm ((dist_le <| le_max_iff.2 <| Or.inl dist_nonneg).2 fun x => ?_) (max_le ?_ ?_)
  ¬∑ rcases em (‚àÉ y, f y = x) with (‚ü®x, rfl‚ü© | hx)
    ¬∑ simp only [extend_apply]
      exact (dist_coe_le_dist x).trans (le_max_left _ _)
    ¬∑ simp only [extend_apply' hx]
      lift x to ((range f)·∂ú : Set Œ¥) using hx
      calc
        dist (h‚ÇÅ x) (h‚ÇÇ x) = dist (h‚ÇÅ.restrict (range f)·∂ú x) (h‚ÇÇ.restrict (range f)·∂ú x) := rfl
        _ ‚â§ dist (h‚ÇÅ.restrict (range f)·∂ú) (h‚ÇÇ.restrict (range f)·∂ú) := dist_coe_le_dist x
        _ ‚â§ _ := le_max_right _ _
  ¬∑ refine (dist_le dist_nonneg).2 fun x => ?_
    rw [‚Üê extend_apply f g‚ÇÅ h‚ÇÅ, ‚Üê extend_apply f g‚ÇÇ h‚ÇÇ]
    exact dist_coe_le_dist _
  ¬∑ refine (dist_le dist_nonneg).2 fun x => ?_
    calc
      dist (h‚ÇÅ x) (h‚ÇÇ x) = dist (extend f g‚ÇÅ h‚ÇÅ x) (extend f g‚ÇÇ h‚ÇÇ x) := by
        rw [extend_apply' x.coe_prop, extend_apply' x.coe_prop]
      _ ‚â§ _ := dist_coe_le_dist _

theorem isometry_extend (f : Œ± ‚Ü™ Œ¥) (h : Œ¥ ‚Üí·µá Œ≤) : Isometry fun g : Œ± ‚Üí·µá Œ≤ => extend f g h :=
  Isometry.of_dist_eq fun g‚ÇÅ g‚ÇÇ => by simp

end Extend

/-- The indicator function of a clopen set, as a bounded continuous function. -/
@[simps]
noncomputable def indicator (s : Set Œ±) (hs : IsClopen s) : BoundedContinuousFunction Œ± ‚Ñù where
  toFun := s.indicator 1
  continuous_toFun := continuous_indicator (by simp [hs]) <| continuous_const.continuousOn
  map_bounded' := ‚ü®1, fun x y ‚Ü¶ by by_cases hx : x ‚àà s <;> by_cases hy : y ‚àà s <;> simp [hx, hy]‚ü©

end Basics

section One

variable [TopologicalSpace Œ±] [PseudoMetricSpace Œ≤] [One Œ≤]

@[to_additive] instance instOne : One (Œ± ‚Üí·µá Œ≤) := ‚ü®const Œ± 1‚ü©

@[to_additive (attr := simp)]
theorem coe_one : ((1 : Œ± ‚Üí·µá Œ≤) : Œ± ‚Üí Œ≤) = 1 := rfl

@[to_additive (attr := simp)]
theorem mkOfCompact_one [CompactSpace Œ±] : mkOfCompact (1 : C(Œ±, Œ≤)) = 1 := rfl

@[to_additive]
theorem forall_coe_one_iff_one (f : Œ± ‚Üí·µá Œ≤) : (‚àÄ x, f x = 1) ‚Üî f = 1 :=
  (@DFunLike.ext_iff _ _ _ _ f 1).symm

@[to_additive (attr := simp)]
theorem one_compContinuous [TopologicalSpace Œ≥] (f : C(Œ≥, Œ±)) : (1 : Œ± ‚Üí·µá Œ≤).compContinuous f = 1 :=
  rfl

end One

section mul

variable {R : Type*} [TopologicalSpace Œ±] [PseudoMetricSpace R]

@[to_additive]
instance instMul [Mul R] [BoundedMul R] [ContinuousMul R] :
    Mul (Œ± ‚Üí·µá R) where
  mul f g :=
    { toFun := fun x ‚Ü¶ f x * g x
      continuous_toFun := f.continuous.mul g.continuous
      map_bounded' := mul_bounded_of_bounded_of_bounded (map_bounded f) (map_bounded g) }

@[to_additive (attr := simp)]
theorem coe_mul [Mul R] [BoundedMul R] [ContinuousMul R] (f g : Œ± ‚Üí·µá R) : ‚áë(f * g) = f * g := rfl

@[to_additive]
theorem mul_apply [Mul R] [BoundedMul R] [ContinuousMul R] (f g : Œ± ‚Üí·µá R) (x : Œ±) :
    (f * g) x = f x * g x := rfl

@[simp]
theorem coe_nsmulRec [PseudoMetricSpace Œ≤] [AddMonoid Œ≤] [BoundedAdd Œ≤] [ContinuousAdd Œ≤]
    (f : Œ± ‚Üí·µá Œ≤) : ‚àÄ n, ‚áë(nsmulRec n f) = n ‚Ä¢ ‚áëf
  | 0 => by rw [nsmulRec, zero_smul, coe_zero]
  | n + 1 => by rw [nsmulRec, succ_nsmul, coe_add, coe_nsmulRec _ n]

instance instSMulNat [PseudoMetricSpace Œ≤] [AddMonoid Œ≤] [BoundedAdd Œ≤] [ContinuousAdd Œ≤] :
    SMul ‚Ñï (Œ± ‚Üí·µá Œ≤) where
  smul n f :=
    { toContinuousMap := n ‚Ä¢ f.toContinuousMap
      map_bounded' := by simpa [coe_nsmulRec] using (nsmulRec n f).map_bounded' }

@[to_additive existing instSMulNat]
instance instPow [Monoid R] [BoundedMul R] [ContinuousMul R] : Pow (Œ± ‚Üí·µá R) ‚Ñï where
  pow f n :=
    { toFun := fun x ‚Ü¶ (f x) ^ n
      continuous_toFun := f.continuous.pow n
      map_bounded' := by
        obtain ‚ü®C, hC‚ü© := Metric.isBounded_iff.mp <| isBounded_pow (isBounded_range f) n
        exact ‚ü®C, fun x y ‚Ü¶ hC (by simp) (by simp)‚ü© }

@[to_additive]
theorem coe_pow [Monoid R] [BoundedMul R] [ContinuousMul R] (n : ‚Ñï) (f : Œ± ‚Üí·µá R) :
    ‚áë(f ^ n) = (‚áëf) ^ n := rfl

@[to_additive (attr := simp)]
theorem pow_apply [Monoid R] [BoundedMul R] [ContinuousMul R] (n : ‚Ñï) (f : Œ± ‚Üí·µá R) (x : Œ±) :
    (f ^ n) x = f x ^ n := rfl

@[to_additive]
instance instMonoid [Monoid R] [BoundedMul R] [ContinuousMul R] :
    Monoid (Œ± ‚Üí·µá R) :=
  Injective.monoid _ DFunLike.coe_injective' rfl (fun _ _ ‚Ü¶ rfl) (fun _ _ ‚Ü¶ rfl)

@[to_additive]
instance instCommMonoid [CommMonoid R] [BoundedMul R] [ContinuousMul R] :
    CommMonoid (Œ± ‚Üí·µá R) where
  __ := instMonoid
  mul_comm f g := by ext x; simp [mul_apply, mul_comm]

/-- Coercion of a `BoundedContinuousFunction` is an `MonoidHom`. Similar to `MonoidHom.coeFn`. -/
@[to_additive (attr := simps) /-- Coercion of a `BoundedContinuousFunction` is an `AddMonoidHom`.
Similar to `AddMonoidHom.coeFn`. -/]
def coeFnMulHom [Monoid R] [BoundedMul R] [ContinuousMul R] : (Œ± ‚Üí·µá R) ‚Üí* Œ± ‚Üí R where
  toFun := (‚áë)
  map_one' := coe_one
  map_mul' := coe_mul

variable (Œ± R) in
/-- The multiplicative map forgetting that a bounded continuous function is bounded. -/
@[to_additive (attr := simps) /-- The additive map forgetting that a bounded continuous
function is bounded.-/]
def toContinuousMapMulHom [Monoid R] [BoundedMul R] [ContinuousMul R] : (Œ± ‚Üí·µá R) ‚Üí* C(Œ±, R) where
  toFun := toContinuousMap
  map_one' := rfl
  map_mul' := by
    intros
    ext
    simp

@[to_additive (attr := simp)]
lemma coe_prod {Œπ : Type*} (s : Finset Œπ) [CommMonoid R] [BoundedMul R] [ContinuousMul R]
    (f : Œπ ‚Üí Œ± ‚Üí·µá R) :
    ‚áë(‚àè i ‚àà s, f i) = ‚àè i ‚àà s, ‚áë(f i) := map_prod coeFnMulHom f s

@[to_additive]
lemma prod_apply {Œπ : Type*} (s : Finset Œπ) [CommMonoid R] [BoundedMul R] [ContinuousMul R]
    (f : Œπ ‚Üí Œ± ‚Üí·µá R) (a : Œ±) :
    (‚àè i ‚àà s, f i) a = ‚àè i ‚àà s, f i a := by simp

@[to_additive]
instance instMulOneClass [MulOneClass R] [BoundedMul R] [ContinuousMul R] : MulOneClass (Œ± ‚Üí·µá R) :=
  DFunLike.coe_injective.mulOneClass _ coe_one coe_mul

/-- Composition on the left by a (lipschitz-continuous) homomorphism of topological monoids, as a
`MonoidHom`. Similar to `MonoidHom.compLeftContinuous`. -/
@[to_additive (attr := simps)
/-- Composition on the left by a (lipschitz-continuous) homomorphism of topological `AddMonoid`s,
as a `AddMonoidHom`. Similar to `AddMonoidHom.compLeftContinuous`. -/]
protected def _root_.MonoidHom.compLeftContinuousBounded (Œ± : Type*)
    [TopologicalSpace Œ±] [PseudoMetricSpace Œ≤] [Monoid Œ≤] [BoundedMul Œ≤] [ContinuousMul Œ≤]
    [PseudoMetricSpace Œ≥] [Monoid Œ≥] [BoundedMul Œ≥] [ContinuousMul Œ≥]
    (g : Œ≤ ‚Üí* Œ≥) {C : NNReal} (hg : LipschitzWith C g) :
    (Œ± ‚Üí·µá Œ≤) ‚Üí* (Œ± ‚Üí·µá Œ≥) where
  toFun f := f.comp g hg
  map_one' := ext fun _ => g.map_one
  map_mul' _ _ := ext fun _ => g.map_mul _ _

end mul

section add

variable [TopologicalSpace Œ±] [PseudoMetricSpace Œ≤]
variable {C : ‚Ñù}

@[simp]
theorem mkOfCompact_add [CompactSpace Œ±] [Add Œ≤] [BoundedAdd Œ≤] [ContinuousAdd Œ≤] (f g : C(Œ±, Œ≤)) :
    mkOfCompact (f + g) = mkOfCompact f + mkOfCompact g := rfl

theorem add_compContinuous [Add Œ≤] [BoundedAdd Œ≤] [ContinuousAdd Œ≤] [TopologicalSpace Œ≥]
    (f g : Œ± ‚Üí·µá Œ≤) (h : C(Œ≥, Œ±)) :
    (g + f).compContinuous h = g.compContinuous h + f.compContinuous h := rfl

end add

section LipschitzAdd

/- In this section, if `Œ≤` is an `AddMonoid` whose addition operation is Lipschitz, then we show
that the space of bounded continuous functions from `Œ±` to `Œ≤` inherits a topological `AddMonoid`
structure, by using pointwise operations and checking that they are compatible with the uniform
distance.

Implementation note: The material in this section could have been written for `LipschitzMul`
and transported by `@[to_additive]`. We choose not to do this because this causes a few lemma
names (for example, `coe_mul`) to conflict with later lemma names for normed rings; this is only a
trivial inconvenience, but in any case there are no obvious applications of the multiplicative
version. -/

variable [TopologicalSpace Œ±] [PseudoMetricSpace Œ≤] [AddMonoid Œ≤] [LipschitzAdd Œ≤]
variable (f g : Œ± ‚Üí·µá Œ≤) {x : Œ±} {C : ‚Ñù}

instance instLipschitzAdd : LipschitzAdd (Œ± ‚Üí·µá Œ≤) where
  lipschitz_add :=
    ‚ü®LipschitzAdd.C Œ≤, by
      have C_nonneg := (LipschitzAdd.C Œ≤).coe_nonneg
      rw [lipschitzWith_iff_dist_le_mul]
      rintro ‚ü®f‚ÇÅ, g‚ÇÅ‚ü© ‚ü®f‚ÇÇ, g‚ÇÇ‚ü©
      rw [dist_le (mul_nonneg C_nonneg dist_nonneg)]
      intro x
      refine le_trans (lipschitz_with_lipschitz_const_add ‚ü®f‚ÇÅ x, g‚ÇÅ x‚ü© ‚ü®f‚ÇÇ x, g‚ÇÇ x‚ü©) ?_
      gcongr
      apply max_le_max <;> exact dist_coe_le_dist x‚ü©

end LipschitzAdd

section sub

variable [TopologicalSpace Œ±]
variable {R : Type*} [PseudoMetricSpace R] [Sub R] [BoundedSub R] [ContinuousSub R]
variable (f g : Œ± ‚Üí·µá R)

/-- The pointwise difference of two bounded continuous functions is again bounded continuous. -/
instance instSub : Sub (Œ± ‚Üí·µá R) where
  sub f g :=
    { toFun := fun x ‚Ü¶ (f x - g x),
      map_bounded' := sub_bounded_of_bounded_of_bounded f.map_bounded' g.map_bounded' }

theorem sub_apply {x : Œ±} : (f - g) x = f x - g x := rfl

@[simp]
theorem coe_sub : ‚áë(f - g) = f - g := rfl

end sub

section casts

variable [TopologicalSpace Œ±] {Œ≤ : Type*} [PseudoMetricSpace Œ≤]

instance [NatCast Œ≤] : NatCast (Œ± ‚Üí·µá Œ≤) := ‚ü®fun n ‚Ü¶ BoundedContinuousFunction.const _ n‚ü©

@[simp]
theorem natCast_apply [NatCast Œ≤] (n : ‚Ñï) (x : Œ±) : (n : Œ± ‚Üí·µá Œ≤) x = n := rfl

instance [IntCast Œ≤] : IntCast (Œ± ‚Üí·µá Œ≤) := ‚ü®fun m ‚Ü¶ BoundedContinuousFunction.const _ m‚ü©

@[simp]
theorem intCast_apply [IntCast Œ≤] (m : ‚Ñ§) (x : Œ±) : (m : Œ± ‚Üí·µá Œ≤) x = m := rfl

end casts

instance instSemiring {R : Type*} [TopologicalSpace Œ±] [PseudoMetricSpace R]
    [Semiring R] [BoundedMul R] [ContinuousMul R] [BoundedAdd R] [ContinuousAdd R] :
    Semiring (Œ± ‚Üí·µá R) :=
  Injective.semiring _ DFunLike.coe_injective'
    rfl rfl (fun _ _ ‚Ü¶ rfl) (fun _ _ ‚Ü¶ rfl) (fun _ _ ‚Ü¶ rfl) (fun _ _ ‚Ü¶ rfl) (fun _ ‚Ü¶ rfl)

section IsBoundedSMul

/-!
### `IsBoundedSMul` (in particular, topological module) structure

In this section, if `Œ≤` is a metric space and a `ùïú`-module whose addition and scalar multiplication
are compatible with the metric structure, then we show that the space of bounded continuous
functions from `Œ±` to `Œ≤` inherits a so-called `IsBoundedSMul` structure (in particular, a
`ContinuousMul` structure, which is the mathlib formulation of being a topological module), by
using pointwise operations and checking that they are compatible with the uniform distance. -/


variable {ùïú : Type*} [PseudoMetricSpace ùïú] [TopologicalSpace Œ±] [PseudoMetricSpace Œ≤]

section SMul

variable [Zero ùïú] [Zero Œ≤] [SMul ùïú Œ≤] [IsBoundedSMul ùïú Œ≤]

instance instSMul : SMul ùïú (Œ± ‚Üí·µá Œ≤) where
  smul c f :=
    { toContinuousMap := c ‚Ä¢ f.toContinuousMap
      map_bounded' :=
        let ‚ü®b, hb‚ü© := f.bounded
        ‚ü®dist c 0 * b, fun x y => by
          refine (dist_smul_pair c (f x) (f y)).trans ?_
          gcongr
          apply hb‚ü© }

@[simp]
theorem coe_smul (c : ùïú) (f : Œ± ‚Üí·µá Œ≤) : ‚áë(c ‚Ä¢ f) = fun x => c ‚Ä¢ f x := rfl

theorem smul_apply (c : ùïú) (f : Œ± ‚Üí·µá Œ≤) (x : Œ±) : (c ‚Ä¢ f) x = c ‚Ä¢ f x := rfl

instance instIsScalarTower {ùïú' : Type*} [PseudoMetricSpace ùïú'] [Zero ùïú'] [SMul ùïú' Œ≤]
    [IsBoundedSMul ùïú' Œ≤] [SMul ùïú' ùïú] [IsScalarTower ùïú' ùïú Œ≤] :
    IsScalarTower ùïú' ùïú (Œ± ‚Üí·µá Œ≤) where
  smul_assoc _ _ _ := ext fun _ ‚Ü¶ smul_assoc ..

instance instSMulCommClass {ùïú' : Type*} [PseudoMetricSpace ùïú'] [Zero ùïú'] [SMul ùïú' Œ≤]
    [IsBoundedSMul ùïú' Œ≤] [SMulCommClass ùïú' ùïú Œ≤] :
    SMulCommClass ùïú' ùïú (Œ± ‚Üí·µá Œ≤) where
  smul_comm _ _ _ := ext fun _ ‚Ü¶ smul_comm ..

instance instIsCentralScalar [SMul ùïú·µê·µí·µñ Œ≤] [IsCentralScalar ùïú Œ≤] : IsCentralScalar ùïú (Œ± ‚Üí·µá Œ≤) where
  op_smul_eq_smul _ _ := ext fun _ => op_smul_eq_smul _ _

instance instIsBoundedSMul : IsBoundedSMul ùïú (Œ± ‚Üí·µá Œ≤) where
  dist_smul_pair' c f‚ÇÅ f‚ÇÇ := by
    rw [dist_le (mul_nonneg dist_nonneg dist_nonneg)]
    intro x
    refine (dist_smul_pair c (f‚ÇÅ x) (f‚ÇÇ x)).trans ?_
    gcongr
    apply dist_coe_le_dist
  dist_pair_smul' c‚ÇÅ c‚ÇÇ f := by
    rw [dist_le (by positivity)]
    intro x
    refine (dist_pair_smul c‚ÇÅ c‚ÇÇ (f x)).trans ?_
    gcongr
    apply dist_coe_le_dist (g := 0)

end SMul

section MulAction

variable [MonoidWithZero ùïú] [Zero Œ≤] [MulAction ùïú Œ≤] [IsBoundedSMul ùïú Œ≤]

instance instMulAction : MulAction ùïú (Œ± ‚Üí·µá Œ≤) :=
  DFunLike.coe_injective.mulAction _ coe_smul

end MulAction

section DistribMulAction

variable [MonoidWithZero ùïú] [AddMonoid Œ≤] [DistribMulAction ùïú Œ≤] [IsBoundedSMul ùïú Œ≤]
variable [BoundedAdd Œ≤] [ContinuousAdd Œ≤]

instance instDistribMulAction : DistribMulAction ùïú (Œ± ‚Üí·µá Œ≤) :=
  DFunLike.coe_injective.distribMulAction ‚ü®‚ü®_, coe_zero‚ü©, coe_add‚ü© coe_smul

end DistribMulAction

section Module

variable [Semiring ùïú] [AddCommMonoid Œ≤] [Module ùïú Œ≤] [IsBoundedSMul ùïú Œ≤]
variable {f g : Œ± ‚Üí·µá Œ≤} {x : Œ±} {C : ‚Ñù}
variable [BoundedAdd Œ≤] [ContinuousAdd Œ≤]

instance instModule : Module ùïú (Œ± ‚Üí·µá Œ≤) :=
  DFunLike.coe_injective.module _ ‚ü®‚ü®_, coe_zero‚ü©, coe_add‚ü©  coe_smul

variable (ùïú)

/-- The evaluation at a point, as a continuous linear map from `Œ± ‚Üí·µá Œ≤` to `Œ≤`. -/
@[simps]
def evalCLM (x : Œ±) : (Œ± ‚Üí·µá Œ≤) ‚ÜíL[ùïú] Œ≤ where
  toFun f := f x
  map_add' _ _ := add_apply _ _ _
  map_smul' _ _ := smul_apply _ _ _

variable (Œ± Œ≤)

/-- The linear map forgetting that a bounded continuous function is bounded. -/
@[simps]
def toContinuousMapLinearMap : (Œ± ‚Üí·µá Œ≤) ‚Üí‚Çó[ùïú] C(Œ±, Œ≤) where
  toFun := toContinuousMap
  map_smul' _ _ := rfl
  map_add' _ _ := rfl

end Module

end IsBoundedSMul

theorem NNReal.upper_bound {Œ± : Type*} [TopologicalSpace Œ±] (f : Œ± ‚Üí·µá ‚Ñù‚â•0) (x : Œ±) :
    f x ‚â§ nndist f 0 := by
  have key : nndist (f x) ((0 : Œ± ‚Üí·µá ‚Ñù‚â•0) x) ‚â§ nndist f 0 := @dist_coe_le_dist Œ± ‚Ñù‚â•0 _ _ f 0 x
  simp only [coe_zero, Pi.zero_apply] at key
  rwa [NNReal.nndist_zero_eq_val' (f x)] at key

end BoundedContinuousFunction
