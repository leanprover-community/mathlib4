/-
Copyright (c) 2018 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Mario Carneiro, Yury Kudryashov, Heather Macbeth
-/
import Mathlib.Algebra.Module.MinimalAxioms
import Mathlib.Analysis.Normed.Order.Lattice
import Mathlib.Analysis.Normed.Operator.Basic
import Mathlib.Topology.ContinuousMap.Bounded.Basic

/-!
# Inheritance of normed algebraic structures by bounded continuous functions

For various types of normed algebraic structures `Œ≤`, we show in this file that the space of
bounded continuous functions from `Œ±` to `Œ≤` inherits the same normed structure, by using
pointwise operations and checking that they are compatible with the uniform distance.
-/

assert_not_exists CStarRing

noncomputable section

open NNReal Set Function

universe u v w

variable {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w}

namespace BoundedContinuousFunction

section NormedAddCommGroup

variable [TopologicalSpace Œ±] [SeminormedAddCommGroup Œ≤]
variable (f g : Œ± ‚Üí·µá Œ≤) {x : Œ±} {C : ‚Ñù}

instance instNorm : Norm (Œ± ‚Üí·µá Œ≤) := ‚ü®(dist ¬∑ 0)‚ü©

theorem norm_def : ‚Äñf‚Äñ = dist f 0 := rfl

/-- The norm of a bounded continuous function is the supremum of `‚Äñf x‚Äñ`.
We use `sInf` to ensure that the definition works if `Œ±` has no elements. -/
theorem norm_eq (f : Œ± ‚Üí·µá Œ≤) : ‚Äñf‚Äñ = sInf { C : ‚Ñù | 0 ‚â§ C ‚àß ‚àÄ x : Œ±, ‚Äñf x‚Äñ ‚â§ C } := by
  simp [norm_def, BoundedContinuousFunction.dist_eq]

/-- When the domain is non-empty, we do not need the `0 ‚â§ C` condition in the formula for `‚Äñf‚Äñ` as a
`sInf`. -/
theorem norm_eq_of_nonempty [h : Nonempty Œ±] : ‚Äñf‚Äñ = sInf { C : ‚Ñù | ‚àÄ x : Œ±, ‚Äñf x‚Äñ ‚â§ C } := by
  obtain ‚ü®a‚ü© := h
  rw [norm_eq]
  congr
  ext
  simp only [and_iff_right_iff_imp]
  exact fun h' => le_trans (norm_nonneg (f a)) (h' a)

@[simp]
theorem norm_eq_zero_of_empty [IsEmpty Œ±] : ‚Äñf‚Äñ = 0 :=
  dist_zero_of_empty

theorem norm_coe_le_norm (x : Œ±) : ‚Äñf x‚Äñ ‚â§ ‚Äñf‚Äñ :=
  calc
    ‚Äñf x‚Äñ = dist (f x) ((0 : Œ± ‚Üí·µá Œ≤) x) := by simp [dist_zero_right]
    _ ‚â§ ‚Äñf‚Äñ := dist_coe_le_dist _

lemma neg_norm_le_apply (f : Œ± ‚Üí·µá ‚Ñù) (x : Œ±) :
    -‚Äñf‚Äñ ‚â§ f x := (abs_le.mp (norm_coe_le_norm f x)).1

lemma apply_le_norm (f : Œ± ‚Üí·µá ‚Ñù) (x : Œ±) :
    f x ‚â§ ‚Äñf‚Äñ := (abs_le.mp (norm_coe_le_norm f x)).2

theorem dist_le_two_norm' {f : Œ≥ ‚Üí Œ≤} {C : ‚Ñù} (hC : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C) (x y : Œ≥) :
    dist (f x) (f y) ‚â§ 2 * C :=
  calc
    dist (f x) (f y) ‚â§ ‚Äñf x‚Äñ + ‚Äñf y‚Äñ := dist_le_norm_add_norm _ _
    _ ‚â§ C + C := add_le_add (hC x) (hC y)
    _ = 2 * C := (two_mul _).symm

/-- Distance between the images of any two points is at most twice the norm of the function. -/
theorem dist_le_two_norm (x y : Œ±) : dist (f x) (f y) ‚â§ 2 * ‚Äñf‚Äñ :=
  dist_le_two_norm' f.norm_coe_le_norm x y

variable {f}

/-- The norm of a function is controlled by the supremum of the pointwise norms. -/
theorem norm_le (C0 : (0 : ‚Ñù) ‚â§ C) : ‚Äñf‚Äñ ‚â§ C ‚Üî ‚àÄ x : Œ±, ‚Äñf x‚Äñ ‚â§ C := by
  simpa using @dist_le _ _ _ _ f 0 _ C0

theorem norm_le_of_nonempty [Nonempty Œ±] {f : Œ± ‚Üí·µá Œ≤} {M : ‚Ñù} : ‚Äñf‚Äñ ‚â§ M ‚Üî ‚àÄ x, ‚Äñf x‚Äñ ‚â§ M := by
  simp_rw [norm_def, ‚Üê dist_zero_right]
  exact dist_le_iff_of_nonempty

theorem norm_lt_iff_of_compact [CompactSpace Œ±] {f : Œ± ‚Üí·µá Œ≤} {M : ‚Ñù} (M0 : 0 < M) :
    ‚Äñf‚Äñ < M ‚Üî ‚àÄ x, ‚Äñf x‚Äñ < M := by
  simp_rw [norm_def, ‚Üê dist_zero_right]
  exact dist_lt_iff_of_compact M0

theorem norm_lt_iff_of_nonempty_compact [Nonempty Œ±] [CompactSpace Œ±] {f : Œ± ‚Üí·µá Œ≤} {M : ‚Ñù} :
    ‚Äñf‚Äñ < M ‚Üî ‚àÄ x, ‚Äñf x‚Äñ < M := by
  simp_rw [norm_def, ‚Üê dist_zero_right]
  exact dist_lt_iff_of_nonempty_compact

variable (f)

/-- Norm of `const Œ± b` is less than or equal to `‚Äñb‚Äñ`. If `Œ±` is nonempty,
then it is equal to `‚Äñb‚Äñ`. -/
theorem norm_const_le (b : Œ≤) : ‚Äñconst Œ± b‚Äñ ‚â§ ‚Äñb‚Äñ :=
  (norm_le (norm_nonneg b)).2 fun _ => le_rfl

@[simp]
theorem norm_const_eq [h : Nonempty Œ±] (b : Œ≤) : ‚Äñconst Œ± b‚Äñ = ‚Äñb‚Äñ :=
  le_antisymm (norm_const_le b) <| h.elim fun x => (const Œ± b).norm_coe_le_norm x

/-- Constructing a bounded continuous function from a uniformly bounded continuous
function taking values in a normed group. -/
def ofNormedAddCommGroup {Œ± : Type u} {Œ≤ : Type v} [TopologicalSpace Œ±] [SeminormedAddCommGroup Œ≤]
    (f : Œ± ‚Üí Œ≤) (Hf : Continuous f) (C : ‚Ñù) (H : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C) : Œ± ‚Üí·µá Œ≤ :=
  ‚ü®‚ü®fun n => f n, Hf‚ü©, ‚ü®_, dist_le_two_norm' H‚ü©‚ü©

@[simp]
theorem coe_ofNormedAddCommGroup {Œ± : Type u} {Œ≤ : Type v} [TopologicalSpace Œ±]
    [SeminormedAddCommGroup Œ≤] (f : Œ± ‚Üí Œ≤) (Hf : Continuous f) (C : ‚Ñù) (H : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C) :
    (ofNormedAddCommGroup f Hf C H : Œ± ‚Üí Œ≤) = f := rfl

theorem norm_ofNormedAddCommGroup_le {f : Œ± ‚Üí Œ≤} (hfc : Continuous f) {C : ‚Ñù} (hC : 0 ‚â§ C)
    (hfC : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C) : ‚ÄñofNormedAddCommGroup f hfc C hfC‚Äñ ‚â§ C :=
  (norm_le hC).2 hfC

/-- Constructing a bounded continuous function from a uniformly bounded
function on a discrete space, taking values in a normed group. -/
def ofNormedAddCommGroupDiscrete {Œ± : Type u} {Œ≤ : Type v} [TopologicalSpace Œ±] [DiscreteTopology Œ±]
    [SeminormedAddCommGroup Œ≤] (f : Œ± ‚Üí Œ≤) (C : ‚Ñù) (H : ‚àÄ x, norm (f x) ‚â§ C) : Œ± ‚Üí·µá Œ≤ :=
  ofNormedAddCommGroup f continuous_of_discreteTopology C H

@[simp]
theorem coe_ofNormedAddCommGroupDiscrete {Œ± : Type u} {Œ≤ : Type v} [TopologicalSpace Œ±]
    [DiscreteTopology Œ±] [SeminormedAddCommGroup Œ≤] (f : Œ± ‚Üí Œ≤) (C : ‚Ñù) (H : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C) :
    (ofNormedAddCommGroupDiscrete f C H : Œ± ‚Üí Œ≤) = f := rfl

/-- Taking the pointwise norm of a bounded continuous function with values in a
`SeminormedAddCommGroup` yields a bounded continuous function with values in ‚Ñù. -/
def normComp : Œ± ‚Üí·µá ‚Ñù :=
  f.comp norm lipschitzWith_one_norm

@[simp]
theorem coe_normComp : (f.normComp : Œ± ‚Üí ‚Ñù) = norm ‚àò f := rfl

@[simp]
theorem norm_normComp : ‚Äñf.normComp‚Äñ = ‚Äñf‚Äñ := by
  simp only [norm_eq, coe_normComp, norm_norm, Function.comp]

theorem bddAbove_range_norm_comp : BddAbove <| Set.range <| norm ‚àò f :=
  (@isBounded_range _ _ _ _ f.normComp).bddAbove

theorem norm_eq_iSup_norm : ‚Äñf‚Äñ = ‚®Ü x : Œ±, ‚Äñf x‚Äñ := by
  simp_rw [norm_def, dist_eq_iSup, coe_zero, Pi.zero_apply, dist_zero_right]

/-- If `‚Äñ(1 : Œ≤)‚Äñ = 1`, then `‚Äñ(1 : Œ± ‚Üí·µá Œ≤)‚Äñ = 1` if `Œ±` is nonempty. -/
instance instNormOneClass [Nonempty Œ±] [One Œ≤] [NormOneClass Œ≤] : NormOneClass (Œ± ‚Üí·µá Œ≤) where
  norm_one := by simp only [norm_eq_iSup_norm, coe_one, Pi.one_apply, norm_one, ciSup_const]

/-- The pointwise opposite of a bounded continuous function is again bounded continuous. -/
instance : Neg (Œ± ‚Üí·µá Œ≤) :=
  ‚ü®fun f =>
    ofNormedAddCommGroup (-f) f.continuous.neg ‚Äñf‚Äñ fun x =>
      norm_neg ((‚áëf) x) ‚ñ∏ f.norm_coe_le_norm x‚ü©

@[simp]
theorem coe_neg : ‚áë(-f) = -f := rfl

theorem neg_apply : (-f) x = -f x := rfl

@[simp]
theorem mkOfCompact_neg [CompactSpace Œ±] (f : C(Œ±, Œ≤)) : mkOfCompact (-f) = -mkOfCompact f := rfl

@[simp]
theorem mkOfCompact_sub [CompactSpace Œ±] (f g : C(Œ±, Œ≤)) :
    mkOfCompact (f - g) = mkOfCompact f - mkOfCompact g := rfl

@[simp]
theorem coe_zsmulRec : ‚àÄ z, ‚áë(zsmulRec (¬∑ ‚Ä¢ ¬∑) z f) = z ‚Ä¢ ‚áëf
  | Int.ofNat n => by rw [zsmulRec, Int.ofNat_eq_coe, coe_nsmul, natCast_zsmul]
  | Int.negSucc n => by rw [zsmulRec, negSucc_zsmul, coe_neg, coe_nsmul]

instance instSMulInt : SMul ‚Ñ§ (Œ± ‚Üí·µá Œ≤) where
  smul n f :=
    { toContinuousMap := n ‚Ä¢ f.toContinuousMap
      map_bounded' := by simpa using (zsmulRec (¬∑ ‚Ä¢ ¬∑) n f).map_bounded' }

@[simp]
theorem coe_zsmul (r : ‚Ñ§) (f : Œ± ‚Üí·µá Œ≤) : ‚áë(r ‚Ä¢ f) = r ‚Ä¢ ‚áëf := rfl

@[simp]
theorem zsmul_apply (r : ‚Ñ§) (f : Œ± ‚Üí·µá Œ≤) (v : Œ±) : (r ‚Ä¢ f) v = r ‚Ä¢ f v := rfl

instance instAddCommGroup : AddCommGroup (Œ± ‚Üí·µá Œ≤) := fast_instance%
  DFunLike.coe_injective.addCommGroup _ coe_zero coe_add coe_neg coe_sub (fun _ _ => coe_nsmul _ _)
    fun _ _ => coe_zsmul _ _

instance instSeminormedAddCommGroup : SeminormedAddCommGroup (Œ± ‚Üí·µá Œ≤) where
  dist_eq f g := by simp only [norm_eq, dist_eq, dist_eq_norm, sub_apply]

instance instNormedAddCommGroup {Œ± Œ≤} [TopologicalSpace Œ±] [NormedAddCommGroup Œ≤] :
    NormedAddCommGroup (Œ± ‚Üí·µá Œ≤) :=
  { instSeminormedAddCommGroup with
    eq_of_dist_eq_zero }

theorem nnnorm_def : ‚Äñf‚Äñ‚Çä = nndist f 0 := rfl

theorem nnnorm_coe_le_nnnorm (x : Œ±) : ‚Äñf x‚Äñ‚Çä ‚â§ ‚Äñf‚Äñ‚Çä :=
  norm_coe_le_norm _ _

theorem nndist_le_two_nnnorm (x y : Œ±) : nndist (f x) (f y) ‚â§ 2 * ‚Äñf‚Äñ‚Çä :=
  dist_le_two_norm _ _ _

/-- The `nnnorm` of a function is controlled by the supremum of the pointwise `nnnorm`s. -/
theorem nnnorm_le (C : ‚Ñù‚â•0) : ‚Äñf‚Äñ‚Çä ‚â§ C ‚Üî ‚àÄ x : Œ±, ‚Äñf x‚Äñ‚Çä ‚â§ C :=
  norm_le C.prop

theorem nnnorm_const_le (b : Œ≤) : ‚Äñconst Œ± b‚Äñ‚Çä ‚â§ ‚Äñb‚Äñ‚Çä :=
  norm_const_le _

@[simp]
theorem nnnorm_const_eq [Nonempty Œ±] (b : Œ≤) : ‚Äñconst Œ± b‚Äñ‚Çä = ‚Äñb‚Äñ‚Çä :=
  Subtype.ext <| norm_const_eq _

theorem nnnorm_eq_iSup_nnnorm : ‚Äñf‚Äñ‚Çä = ‚®Ü x : Œ±, ‚Äñf x‚Äñ‚Çä :=
  Subtype.ext <| (norm_eq_iSup_norm f).trans <| by simp_rw [val_eq_coe, NNReal.coe_iSup, coe_nnnorm]

theorem enorm_eq_iSup_enorm : ‚Äñf‚Äñ‚Çë = ‚®Ü x, ‚Äñf x‚Äñ‚Çë := by
  simpa only [‚Üê edist_zero_eq_enorm] using edist_eq_iSup

theorem abs_diff_coe_le_dist : ‚Äñf x - g x‚Äñ ‚â§ dist f g := by
  rw [dist_eq_norm]
  exact (f - g).norm_coe_le_norm x

theorem coe_le_coe_add_dist {f g : Œ± ‚Üí·µá ‚Ñù} : f x ‚â§ g x + dist f g :=
  sub_le_iff_le_add'.1 <| (abs_le.1 <| @dist_coe_le_dist _ _ _ _ f g x).2

theorem norm_compContinuous_le [TopologicalSpace Œ≥] (f : Œ± ‚Üí·µá Œ≤) (g : C(Œ≥, Œ±)) :
    ‚Äñf.compContinuous g‚Äñ ‚â§ ‚Äñf‚Äñ :=
  ((lipschitz_compContinuous g).dist_le_mul f 0).trans <| by
    rw [NNReal.coe_one, one_mul, dist_zero_right]

end NormedAddCommGroup

section NormedSpace

variable {ùïú : Type*}
variable [TopologicalSpace Œ±] [SeminormedAddCommGroup Œ≤]
variable {f g : Œ± ‚Üí·µá Œ≤} {x : Œ±} {C : ‚Ñù}

instance instNormedSpace [NormedField ùïú] [NormedSpace ùïú Œ≤] : NormedSpace ùïú (Œ± ‚Üí·µá Œ≤) :=
  ‚ü®fun c f => by
    refine norm_ofNormedAddCommGroup_le _ (mul_nonneg (norm_nonneg _) (norm_nonneg _)) ?_
    exact fun x =>
      norm_smul c (f x) ‚ñ∏ mul_le_mul_of_nonneg_left (f.norm_coe_le_norm _) (norm_nonneg _)‚ü©

variable [NontriviallyNormedField ùïú] [NormedSpace ùïú Œ≤]
variable [SeminormedAddCommGroup Œ≥] [NormedSpace ùïú Œ≥]
variable (Œ±)

-- TODO does this work in the `IsBoundedSMul` setting, too?
/-- Postcomposition of bounded continuous functions into a normed module by a continuous linear map
is a continuous linear map.
Upgraded version of `ContinuousLinearMap.compLeftContinuous`, similar to `LinearMap.compLeft`. -/
protected def _root_.ContinuousLinearMap.compLeftContinuousBounded (g : Œ≤ ‚ÜíL[ùïú] Œ≥) :
    (Œ± ‚Üí·µá Œ≤) ‚ÜíL[ùïú] Œ± ‚Üí·µá Œ≥ :=
  LinearMap.mkContinuous
    { toFun := fun f =>
        ofNormedAddCommGroup (g ‚àò f) (g.continuous.comp f.continuous) (‚Äñg‚Äñ * ‚Äñf‚Äñ) fun x =>
          g.le_opNorm_of_le (f.norm_coe_le_norm x)
      map_add' := fun f g => by ext; simp
      map_smul' := fun c f => by ext; simp } ‚Äñg‚Äñ fun f =>
        norm_ofNormedAddCommGroup_le _ (mul_nonneg (norm_nonneg g) (norm_nonneg f))
          (fun x => by exact g.le_opNorm_of_le (f.norm_coe_le_norm x))

@[simp]
theorem _root_.ContinuousLinearMap.compLeftContinuousBounded_apply (g : Œ≤ ‚ÜíL[ùïú] Œ≥) (f : Œ± ‚Üí·µá Œ≤)
    (x : Œ±) : (g.compLeftContinuousBounded Œ± f) x = g (f x) := rfl

end NormedSpace

section NormedRing

variable [TopologicalSpace Œ±] {R : Type*}

section NonUnital

section Seminormed

variable [NonUnitalSeminormedRing R]

instance instNonUnitalRing : NonUnitalRing (Œ± ‚Üí·µá R) := fast_instance%
  DFunLike.coe_injective.nonUnitalRing _ coe_zero coe_add coe_mul coe_neg coe_sub
    (fun _ _ => coe_nsmul _ _) fun _ _ => coe_zsmul _ _

instance instNonUnitalSeminormedRing : NonUnitalSeminormedRing (Œ± ‚Üí·µá R) where
  __ := instSeminormedAddCommGroup
  __ := instNonUnitalRing
  norm_mul_le f g := norm_ofNormedAddCommGroup_le _ (by positivity)
    (fun x ‚Ü¶ (norm_mul_le _ _).trans <| mul_le_mul
      (norm_coe_le_norm f x) (norm_coe_le_norm g x) (norm_nonneg _) (norm_nonneg _))

end Seminormed

instance instNonUnitalSeminormedCommRing [NonUnitalSeminormedCommRing R] :
    NonUnitalSeminormedCommRing (Œ± ‚Üí·µá R) where
  mul_comm _ _ := ext fun _ ‚Ü¶ mul_comm ..

instance instNonUnitalNormedRing [NonUnitalNormedRing R] : NonUnitalNormedRing (Œ± ‚Üí·µá R) where
  __ := instNonUnitalSeminormedRing
  __ := instNormedAddCommGroup

instance instNonUnitalNormedCommRing [NonUnitalNormedCommRing R] :
    NonUnitalNormedCommRing (Œ± ‚Üí·µá R) where
  mul_comm := mul_comm

end NonUnital

section Seminormed

variable [SeminormedRing R]

@[simp]
theorem coe_npowRec (f : Œ± ‚Üí·µá R) : ‚àÄ n, ‚áë(npowRec n f) = (‚áëf) ^ n
  | 0 => by rw [npowRec, pow_zero, coe_one]
  | n + 1 => by rw [npowRec, pow_succ, coe_mul, coe_npowRec f n]

instance hasNatPow : Pow (Œ± ‚Üí·µá R) ‚Ñï where
  pow f n :=
    { toContinuousMap := f.toContinuousMap ^ n
      map_bounded' := by simpa [coe_npowRec] using (npowRec n f).map_bounded' }

instance : NatCast (Œ± ‚Üí·µá R) :=
  ‚ü®fun n => BoundedContinuousFunction.const _ n‚ü©

@[simp, norm_cast]
theorem coe_natCast (n : ‚Ñï) : ((n : Œ± ‚Üí·µá R) : Œ± ‚Üí R) = n := rfl

@[simp, norm_cast]
theorem coe_ofNat (n : ‚Ñï) [n.AtLeastTwo] :
    ((ofNat(n) : Œ± ‚Üí·µá R) : Œ± ‚Üí R) = ofNat(n) :=
  rfl

instance : IntCast (Œ± ‚Üí·µá R) :=
  ‚ü®fun n => BoundedContinuousFunction.const _ n‚ü©

@[simp, norm_cast]
theorem coe_intCast (n : ‚Ñ§) : ((n : Œ± ‚Üí·µá R) : Œ± ‚Üí R) = n := rfl

instance instRing : Ring (Œ± ‚Üí·µá R) := fast_instance%
  DFunLike.coe_injective.ring _ coe_zero coe_one coe_add coe_mul coe_neg coe_sub
    (fun _ _ => coe_nsmul _ _) (fun _ _ => coe_zsmul _ _) (fun _ _ => coe_pow _ _) coe_natCast
    coe_intCast

instance instSeminormedRing : SeminormedRing (Œ± ‚Üí·µá R) where
  __ := instRing
  __ := instNonUnitalSeminormedRing

/-- Composition on the left by a (lipschitz-continuous) homomorphism of topological semirings, as a
`RingHom`. Similar to `RingHom.compLeftContinuous`. -/
@[simps!]
protected def _root_.RingHom.compLeftContinuousBounded (Œ± : Type*)
    [TopologicalSpace Œ±] [SeminormedRing Œ≤] [SeminormedRing Œ≥]
    (g : Œ≤ ‚Üí+* Œ≥) {C : NNReal} (hg : LipschitzWith C g) : (Œ± ‚Üí·µá Œ≤) ‚Üí+* (Œ± ‚Üí·µá Œ≥) :=
  { g.toMonoidHom.compLeftContinuousBounded Œ± hg,
    g.toAddMonoidHom.compLeftContinuousBounded Œ± hg with }

end Seminormed

instance instNormedRing [NormedRing R] : NormedRing (Œ± ‚Üí·µá R) where
  __ := instRing
  __ := instNonUnitalNormedRing

end NormedRing

section NormedCommRing

variable [TopologicalSpace Œ±] {R : Type*}

instance instCommRing [SeminormedCommRing R] : CommRing (Œ± ‚Üí·µá R) where
  mul_comm _ _ := ext fun _ ‚Ü¶ mul_comm _ _

instance instSeminormedCommRing [SeminormedCommRing R] : SeminormedCommRing (Œ± ‚Üí·µá R) where
  __ := instCommRing
  __ := instNonUnitalSeminormedRing

instance instNormedCommRing [NormedCommRing R] : NormedCommRing (Œ± ‚Üí·µá R) where
  __ := instSeminormedCommRing
  __ := instNormedAddCommGroup

end NormedCommRing

section NonUnitalAlgebra

-- these hypotheses could be generalized if we generalize `IsBoundedSMul` to `Bornology`.
variable {ùïú : Type*} [PseudoMetricSpace ùïú] [TopologicalSpace Œ±] [NonUnitalSeminormedRing Œ≤]
variable [Zero ùïú] [SMul ùïú Œ≤] [IsBoundedSMul ùïú Œ≤]

instance [IsScalarTower ùïú Œ≤ Œ≤] : IsScalarTower ùïú (Œ± ‚Üí·µá Œ≤) (Œ± ‚Üí·µá Œ≤) where
  smul_assoc _ _ _ := ext fun _ ‚Ü¶ smul_mul_assoc ..

instance [SMulCommClass ùïú Œ≤ Œ≤] : SMulCommClass ùïú (Œ± ‚Üí·µá Œ≤) (Œ± ‚Üí·µá Œ≤) where
  smul_comm _ _ _ := ext fun _ ‚Ü¶ (mul_smul_comm ..).symm

instance [SMulCommClass ùïú Œ≤ Œ≤] : SMulCommClass (Œ± ‚Üí·µá Œ≤) ùïú (Œ± ‚Üí·µá Œ≤) where
  smul_comm _ _ _ := ext fun _ ‚Ü¶ mul_smul_comm ..

end NonUnitalAlgebra

section NormedAlgebra

variable {ùïú : Type*} [NormedField ùïú]
variable [TopologicalSpace Œ±] [SeminormedAddCommGroup Œ≤] [NormedSpace ùïú Œ≤]
variable [NormedRing Œ≥] [NormedAlgebra ùïú Œ≥]
variable {f g : Œ± ‚Üí·µá Œ≥} {x : Œ±} {c : ùïú}

/-- `BoundedContinuousFunction.const` as a `RingHom`. -/
def C : ùïú ‚Üí+* Œ± ‚Üí·µá Œ≥ where
  toFun := fun c : ùïú => const Œ± ((algebraMap ùïú Œ≥) c)
  map_one' := ext fun _ => (algebraMap ùïú Œ≥).map_one
  map_mul' _ _ := ext fun _ => (algebraMap ùïú Œ≥).map_mul _ _
  map_zero' := ext fun _ => (algebraMap ùïú Œ≥).map_zero
  map_add' _ _ := ext fun _ => (algebraMap ùïú Œ≥).map_add _ _

instance instAlgebra : Algebra ùïú (Œ± ‚Üí·µá Œ≥) where
  algebraMap := C
  commutes' _ _ := ext fun _ ‚Ü¶ Algebra.commutes' _ _
  smul_def' _ _ := ext fun _ ‚Ü¶ Algebra.smul_def' _ _

@[simp]
theorem algebraMap_apply (k : ùïú) (a : Œ±) : algebraMap ùïú (Œ± ‚Üí·µá Œ≥) k a = k ‚Ä¢ (1 : Œ≥) := by
  simp only [Algebra.algebraMap_eq_smul_one, coe_smul, coe_one, Pi.one_apply]

instance instNormedAlgebra : NormedAlgebra ùïú (Œ± ‚Üí·µá Œ≥) where
  __ := instAlgebra
  __ := instNormedSpace

variable (ùïú)

/-- Composition on the left by a (lipschitz-continuous) homomorphism of topological `R`-algebras,
as an `AlgHom`. Similar to `AlgHom.compLeftContinuous`. -/
@[simps!]
protected def AlgHom.compLeftContinuousBounded
    [NormedRing Œ≤] [NormedAlgebra ùïú Œ≤] [NormedRing Œ≥] [NormedAlgebra ùïú Œ≥]
    (g : Œ≤ ‚Üí‚Çê[ùïú] Œ≥) {C : NNReal} (hg : LipschitzWith C g) : (Œ± ‚Üí·µá Œ≤) ‚Üí‚Çê[ùïú] (Œ± ‚Üí·µá Œ≥) :=
  { g.toRingHom.compLeftContinuousBounded Œ± hg with
    commutes' := fun _ => DFunLike.ext _ _ fun _ => g.commutes' _ }

/-- The algebra-homomorphism forgetting that a bounded continuous function is bounded. -/
@[simps]
def toContinuousMap‚Çê : (Œ± ‚Üí·µá Œ≥) ‚Üí‚Çê[ùïú] C(Œ±, Œ≥) where
  toFun := (‚Üë)
  map_one' := rfl
  map_mul' _ _ := rfl
  map_zero' := rfl
  map_add' _ _ := rfl
  commutes' _ := rfl

@[simp]
theorem coe_toContinuousMap‚Çê (f : Œ± ‚Üí·µá Œ≥) : (f.toContinuousMap‚Çê ùïú : Œ± ‚Üí Œ≥) = f := rfl

variable {ùïú}

/-! ### Structure as normed module over scalar functions

If `Œ≤` is a normed `ùïú`-space, then we show that the space of bounded continuous
functions from `Œ±` to `Œ≤` is naturally a module over the algebra of bounded continuous
functions from `Œ±` to `ùïú`. -/

instance instSMul' : SMul (Œ± ‚Üí·µá ùïú) (Œ± ‚Üí·µá Œ≤) where
  smul f g :=
    ofNormedAddCommGroup (fun x => f x ‚Ä¢ g x) (f.continuous.smul g.continuous) (‚Äñf‚Äñ * ‚Äñg‚Äñ) fun x =>
      calc
        ‚Äñf x ‚Ä¢ g x‚Äñ ‚â§ ‚Äñf x‚Äñ * ‚Äñg x‚Äñ := norm_smul_le _ _
        _ ‚â§ ‚Äñf‚Äñ * ‚Äñg‚Äñ :=
          mul_le_mul (f.norm_coe_le_norm _) (g.norm_coe_le_norm _) (norm_nonneg _) (norm_nonneg _)

instance instModule' : Module (Œ± ‚Üí·µá ùïú) (Œ± ‚Üí·µá Œ≤) :=
  Module.ofMinimalAxioms
      (fun c _ _ => ext fun a => smul_add (c a) _ _)
      (fun _ _ _ => ext fun _ => add_smul _ _ _)
      (fun _ _ _ => ext fun _ => mul_smul _ _ _)
      (fun f => ext fun x => one_smul ùïú (f x))

/- TODO: When `NormedModule` has been added to `Analysis.Normed.Module.Basic`, this
shows that the space of bounded continuous functions from `Œ±` to `Œ≤` is naturally a normed
module over the algebra of bounded continuous functions from `Œ±` to `ùïú`. -/
instance : IsBoundedSMul (Œ± ‚Üí·µá ùïú) (Œ± ‚Üí·µá Œ≤) :=
  IsBoundedSMul.of_norm_smul_le fun _ _ =>
    norm_ofNormedAddCommGroup_le _ (mul_nonneg (norm_nonneg _) (norm_nonneg _)) _

end NormedAlgebra

section NormedLatticeOrderedGroup

variable [TopologicalSpace Œ±]
  [NormedAddCommGroup Œ≤] [Lattice Œ≤] [HasSolidNorm Œ≤] [IsOrderedAddMonoid Œ≤]

instance instPartialOrder : PartialOrder (Œ± ‚Üí·µá Œ≤) :=
  PartialOrder.lift (fun f => f.toFun) (by simp [Injective])

instance instSup : Max (Œ± ‚Üí·µá Œ≤) where
  max f g :=
    { toFun := f ‚äî g
      continuous_toFun := f.continuous.sup g.continuous
      map_bounded' := by
        obtain ‚ü®C‚ÇÅ, hf‚ü© := f.bounded
        obtain ‚ü®C‚ÇÇ, hg‚ü© := g.bounded
        refine ‚ü®C‚ÇÅ + C‚ÇÇ, fun x y ‚Ü¶ ?_‚ü©
        simp_rw [dist_eq_norm_sub] at hf hg ‚ä¢
        exact (norm_sup_sub_sup_le_add_norm _ _ _ _).trans (add_le_add (hf _ _) (hg _ _)) }

instance instInf : Min (Œ± ‚Üí·µá Œ≤) where
  min f g :=
    { toFun := f ‚äì g
      continuous_toFun := f.continuous.inf g.continuous
      map_bounded' := by
        obtain ‚ü®C‚ÇÅ, hf‚ü© := f.bounded
        obtain ‚ü®C‚ÇÇ, hg‚ü© := g.bounded
        refine ‚ü®C‚ÇÅ + C‚ÇÇ, fun x y ‚Ü¶ ?_‚ü©
        simp_rw [dist_eq_norm_sub] at hf hg ‚ä¢
        exact (norm_inf_sub_inf_le_add_norm _ _ _ _).trans (add_le_add (hf _ _) (hg _ _)) }

@[simp, norm_cast] lemma coe_sup (f g : Œ± ‚Üí·µá Œ≤) : ‚áë(f ‚äî g) = ‚áëf ‚äî ‚áëg := rfl

@[simp, norm_cast] lemma coe_inf (f g : Œ± ‚Üí·µá Œ≤) : ‚áë(f ‚äì g) = ‚áëf ‚äì ‚áëg := rfl

instance instSemilatticeSup : SemilatticeSup (Œ± ‚Üí·µá Œ≤) := fast_instance%
  DFunLike.coe_injective.semilatticeSup _ .rfl .rfl coe_sup

instance instSemilatticeInf : SemilatticeInf (Œ± ‚Üí·µá Œ≤) := fast_instance%
  DFunLike.coe_injective.semilatticeInf _ .rfl .rfl coe_inf

instance instLattice : Lattice (Œ± ‚Üí·µá Œ≤) := fast_instance%
  DFunLike.coe_injective.lattice _ .rfl .rfl coe_sup coe_inf

@[simp, norm_cast] lemma coe_abs (f : Œ± ‚Üí·µá Œ≤) : ‚áë|f| = |‚áëf| := rfl
@[simp, norm_cast] lemma coe_posPart (f : Œ± ‚Üí·µá Œ≤) : ‚áëf‚Å∫ = (‚áëf)‚Å∫ := rfl
@[simp, norm_cast] lemma coe_negPart (f : Œ± ‚Üí·µá Œ≤) : ‚áëf‚Åª = (‚áëf)‚Åª := rfl

instance instHasSolidNorm : HasSolidNorm (Œ± ‚Üí·µá Œ≤) :=
  { solid := by
      intro f g h
      have i1 : ‚àÄ t, ‚Äñf t‚Äñ ‚â§ ‚Äñg t‚Äñ := fun t => HasSolidNorm.solid (h t)
      rw [norm_le (norm_nonneg _)]
      exact fun t => (i1 t).trans (norm_coe_le_norm g t) }

instance instIsOrderedAddMonoid : IsOrderedAddMonoid (Œ± ‚Üí·µá Œ≤) where
  add_le_add_left f g h‚ÇÅ h t := by simpa using h‚ÇÅ _

end NormedLatticeOrderedGroup

section NonnegativePart

variable [TopologicalSpace Œ±]

/-- The nonnegative part of a bounded continuous `‚Ñù`-valued function as a bounded
continuous `‚Ñù‚â•0`-valued function. -/
def nnrealPart (f : Œ± ‚Üí·µá ‚Ñù) : Œ± ‚Üí·µá ‚Ñù‚â•0 :=
  BoundedContinuousFunction.comp _ (show LipschitzWith 1 Real.toNNReal from lipschitzWith_posPart) f

@[simp]
theorem nnrealPart_coeFn_eq (f : Œ± ‚Üí·µá ‚Ñù) : ‚áëf.nnrealPart = Real.toNNReal ‚àò ‚áëf := rfl

/-- The absolute value of a bounded continuous `‚Ñù`-valued function as a bounded
continuous `‚Ñù‚â•0`-valued function. -/
def nnnorm (f : Œ± ‚Üí·µá ‚Ñù) : Œ± ‚Üí·µá ‚Ñù‚â•0 :=
  BoundedContinuousFunction.comp _
    (show LipschitzWith 1 fun x : ‚Ñù => ‚Äñx‚Äñ‚Çä from lipschitzWith_one_norm) f

@[simp]
theorem nnnorm_coeFn_eq (f : Œ± ‚Üí·µá ‚Ñù) : ‚áëf.nnnorm = NNNorm.nnnorm ‚àò ‚áëf := rfl

-- TODO: Use `posPart` and `negPart` here
/-- Decompose a bounded continuous function to its positive and negative parts. -/
theorem self_eq_nnrealPart_sub_nnrealPart_neg (f : Œ± ‚Üí·µá ‚Ñù) :
    ‚áëf = (‚Üë) ‚àò f.nnrealPart - (‚Üë) ‚àò (-f).nnrealPart := by
  funext x
  dsimp
  simp only [max_zero_sub_max_neg_zero_eq_self]

/-- Express the absolute value of a bounded continuous function in terms of its
positive and negative parts. -/
theorem abs_self_eq_nnrealPart_add_nnrealPart_neg (f : Œ± ‚Üí·µá ‚Ñù) :
    abs ‚àò ‚áëf = (‚Üë) ‚àò f.nnrealPart + (‚Üë) ‚àò (-f).nnrealPart := by
  funext x
  dsimp
  simp only [max_zero_add_max_neg_zero_eq_abs_self]

end NonnegativePart

section

variable {Œ± : Type*} [TopologicalSpace Œ±]

-- TODO: `f + const _ ‚Äñf‚Äñ` is just `f‚Å∫`
lemma add_norm_nonneg (f : Œ± ‚Üí·µá ‚Ñù) :
    0 ‚â§ f + const _ ‚Äñf‚Äñ := by
  intro x
  simp only [ContinuousMap.toFun_eq_coe, coe_toContinuousMap, coe_zero, Pi.zero_apply, coe_add,
    const_apply, Pi.add_apply]
  linarith [(abs_le.mp (norm_coe_le_norm f x)).1]

lemma norm_sub_nonneg (f : Œ± ‚Üí·µá ‚Ñù) :
    0 ‚â§ const _ ‚Äñf‚Äñ - f := by
  intro x
  simp only [ContinuousMap.toFun_eq_coe, coe_toContinuousMap, coe_zero, Pi.zero_apply, coe_sub,
    const_apply, Pi.sub_apply, sub_nonneg]
  linarith [(abs_le.mp (norm_coe_le_norm f x)).2]

end

end BoundedContinuousFunction
