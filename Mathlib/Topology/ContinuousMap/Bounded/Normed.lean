/-
Copyright (c) 2018 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Mario Carneiro, Yury Kudryashov, Heather Macbeth
-/
import Mathlib.Algebra.Module.MinimalAxioms
import Mathlib.Analysis.Normed.Order.Lattice
import Mathlib.Analysis.NormedSpace.OperatorNorm.Basic
import Mathlib.Topology.ContinuousMap.Bounded.Basic

/-!
# Inheritance of normed algebraic structures by bounded continuous functions

For various types of normed algebraic structures `Î²`, we show in this file that the space of
bounded continuous functions from `Î±` to `Î²` inherits the same normed structure, by using
pointwise operations and checking that they are compatible with the uniform distance.
-/

assert_not_exists CStarRing

noncomputable section

open NNReal Set Function

universe u v w

variable {Î± : Type u} {Î² : Type v} {Î³ : Type w}

namespace BoundedContinuousFunction

section NormedAddCommGroup

variable [TopologicalSpace Î±] [SeminormedAddCommGroup Î²]
variable (f g : Î± â†’áµ‡ Î²) {x : Î±} {C : â„}

instance instNorm : Norm (Î± â†’áµ‡ Î²) := âŸ¨(dist Â· 0)âŸ©

theorem norm_def : â€–fâ€– = dist f 0 := rfl

/-- The norm of a bounded continuous function is the supremum of `â€–f xâ€–`.
We use `sInf` to ensure that the definition works if `Î±` has no elements. -/
theorem norm_eq (f : Î± â†’áµ‡ Î²) : â€–fâ€– = sInf { C : â„ | 0 â‰¤ C âˆ§ âˆ€ x : Î±, â€–f xâ€– â‰¤ C } := by
  simp [norm_def, BoundedContinuousFunction.dist_eq]

/-- When the domain is non-empty, we do not need the `0 â‰¤ C` condition in the formula for `â€–fâ€–` as a
`sInf`. -/
theorem norm_eq_of_nonempty [h : Nonempty Î±] : â€–fâ€– = sInf { C : â„ | âˆ€ x : Î±, â€–f xâ€– â‰¤ C } := by
  obtain âŸ¨aâŸ© := h
  rw [norm_eq]
  congr
  ext
  simp only [and_iff_right_iff_imp]
  exact fun h' â†¦ le_trans (norm_nonneg (f a)) (h' a)

@[simp]
theorem norm_eq_zero_of_empty [IsEmpty Î±] : â€–fâ€– = 0 :=
  dist_zero_of_empty

theorem norm_coe_le_norm (x : Î±) : â€–f xâ€– â‰¤ â€–fâ€– :=
  calc
    â€–f xâ€– = dist (f x) ((0 : Î± â†’áµ‡ Î²) x) := by simp [dist_zero_right]
    _ â‰¤ â€–fâ€– := dist_coe_le_dist _

lemma neg_norm_le_apply (f : Î± â†’áµ‡ â„) (x : Î±) :
    -â€–fâ€– â‰¤ f x := (abs_le.mp (norm_coe_le_norm f x)).1

lemma apply_le_norm (f : Î± â†’áµ‡ â„) (x : Î±) :
    f x â‰¤ â€–fâ€– := (abs_le.mp (norm_coe_le_norm f x)).2

theorem dist_le_two_norm' {f : Î³ â†’ Î²} {C : â„} (hC : âˆ€ x, â€–f xâ€– â‰¤ C) (x y : Î³) :
    dist (f x) (f y) â‰¤ 2 * C :=
  calc
    dist (f x) (f y) â‰¤ â€–f xâ€– + â€–f yâ€– := dist_le_norm_add_norm _ _
    _ â‰¤ C + C := add_le_add (hC x) (hC y)
    _ = 2 * C := (two_mul _).symm

/-- Distance between the images of any two points is at most twice the norm of the function. -/
theorem dist_le_two_norm (x y : Î±) : dist (f x) (f y) â‰¤ 2 * â€–fâ€– :=
  dist_le_two_norm' f.norm_coe_le_norm x y

variable {f}

/-- The norm of a function is controlled by the supremum of the pointwise norms. -/
theorem norm_le (C0 : (0 : â„) â‰¤ C) : â€–fâ€– â‰¤ C â†” âˆ€ x : Î±, â€–f xâ€– â‰¤ C := by
  simpa using @dist_le _ _ _ _ f 0 _ C0

theorem norm_le_of_nonempty [Nonempty Î±] {f : Î± â†’áµ‡ Î²} {M : â„} : â€–fâ€– â‰¤ M â†” âˆ€ x, â€–f xâ€– â‰¤ M := by
  simp_rw [norm_def, â† dist_zero_right]
  exact dist_le_iff_of_nonempty

theorem norm_lt_iff_of_compact [CompactSpace Î±] {f : Î± â†’áµ‡ Î²} {M : â„} (M0 : 0 < M) :
    â€–fâ€– < M â†” âˆ€ x, â€–f xâ€– < M := by
  simp_rw [norm_def, â† dist_zero_right]
  exact dist_lt_iff_of_compact M0

theorem norm_lt_iff_of_nonempty_compact [Nonempty Î±] [CompactSpace Î±] {f : Î± â†’áµ‡ Î²} {M : â„} :
    â€–fâ€– < M â†” âˆ€ x, â€–f xâ€– < M := by
  simp_rw [norm_def, â† dist_zero_right]
  exact dist_lt_iff_of_nonempty_compact

variable (f)

/-- Norm of `const Î± b` is less than or equal to `â€–bâ€–`. If `Î±` is nonempty,
then it is equal to `â€–bâ€–`. -/
theorem norm_const_le (b : Î²) : â€–const Î± bâ€– â‰¤ â€–bâ€– :=
  (norm_le (norm_nonneg b)).2 fun _ â†¦ le_rfl

@[simp]
theorem norm_const_eq [h : Nonempty Î±] (b : Î²) : â€–const Î± bâ€– = â€–bâ€– :=
  le_antisymm (norm_const_le b) <| h.elim fun x â†¦ (const Î± b).norm_coe_le_norm x

/-- Constructing a bounded continuous function from a uniformly bounded continuous
function taking values in a normed group. -/
def ofNormedAddCommGroup {Î± : Type u} {Î² : Type v} [TopologicalSpace Î±] [SeminormedAddCommGroup Î²]
    (f : Î± â†’ Î²) (Hf : Continuous f) (C : â„) (H : âˆ€ x, â€–f xâ€– â‰¤ C) : Î± â†’áµ‡ Î² :=
  âŸ¨âŸ¨fun n â†¦ f n, HfâŸ©, âŸ¨_, dist_le_two_norm' HâŸ©âŸ©

@[simp]
theorem coe_ofNormedAddCommGroup {Î± : Type u} {Î² : Type v} [TopologicalSpace Î±]
    [SeminormedAddCommGroup Î²] (f : Î± â†’ Î²) (Hf : Continuous f) (C : â„) (H : âˆ€ x, â€–f xâ€– â‰¤ C) :
    (ofNormedAddCommGroup f Hf C H : Î± â†’ Î²) = f := rfl

theorem norm_ofNormedAddCommGroup_le {f : Î± â†’ Î²} (hfc : Continuous f) {C : â„} (hC : 0 â‰¤ C)
    (hfC : âˆ€ x, â€–f xâ€– â‰¤ C) : â€–ofNormedAddCommGroup f hfc C hfCâ€– â‰¤ C :=
  (norm_le hC).2 hfC

/-- Constructing a bounded continuous function from a uniformly bounded
function on a discrete space, taking values in a normed group. -/
def ofNormedAddCommGroupDiscrete {Î± : Type u} {Î² : Type v} [TopologicalSpace Î±] [DiscreteTopology Î±]
    [SeminormedAddCommGroup Î²] (f : Î± â†’ Î²) (C : â„) (H : âˆ€ x, norm (f x) â‰¤ C) : Î± â†’áµ‡ Î² :=
  ofNormedAddCommGroup f continuous_of_discreteTopology C H

@[simp]
theorem coe_ofNormedAddCommGroupDiscrete {Î± : Type u} {Î² : Type v} [TopologicalSpace Î±]
    [DiscreteTopology Î±] [SeminormedAddCommGroup Î²] (f : Î± â†’ Î²) (C : â„) (H : âˆ€ x, â€–f xâ€– â‰¤ C) :
    (ofNormedAddCommGroupDiscrete f C H : Î± â†’ Î²) = f := rfl

/-- Taking the pointwise norm of a bounded continuous function with values in a
`SeminormedAddCommGroup` yields a bounded continuous function with values in â„. -/
def normComp : Î± â†’áµ‡ â„ :=
  f.comp norm lipschitzWith_one_norm

@[simp]
theorem coe_normComp : (f.normComp : Î± â†’ â„) = norm âˆ˜ f := rfl

@[simp]
theorem norm_normComp : â€–f.normCompâ€– = â€–fâ€– := by
  simp only [norm_eq, coe_normComp, norm_norm, Function.comp]

theorem bddAbove_range_norm_comp : BddAbove <| Set.range <| norm âˆ˜ f :=
  (@isBounded_range _ _ _ _ f.normComp).bddAbove

theorem norm_eq_iSup_norm : â€–fâ€– = â¨† x : Î±, â€–f xâ€– := by
  simp_rw [norm_def, dist_eq_iSup, coe_zero, Pi.zero_apply, dist_zero_right]

/-- If `â€–(1 : Î²)â€– = 1`, then `â€–(1 : Î± â†’áµ‡ Î²)â€– = 1` if `Î±` is nonempty. -/
instance instNormOneClass [Nonempty Î±] [One Î²] [NormOneClass Î²] : NormOneClass (Î± â†’áµ‡ Î²) where
  norm_one := by simp only [norm_eq_iSup_norm, coe_one, Pi.one_apply, norm_one, ciSup_const]

/-- The pointwise opposite of a bounded continuous function is again bounded continuous. -/
instance : Neg (Î± â†’áµ‡ Î²) :=
  âŸ¨fun f â†¦
    ofNormedAddCommGroup (-f) f.continuous.neg â€–fâ€– fun x â†¦
      norm_neg ((â‡‘f) x) â–¸ f.norm_coe_le_norm xâŸ©

@[simp]
theorem coe_neg : â‡‘(-f) = -f := rfl

theorem neg_apply : (-f) x = -f x := rfl

@[simp]
theorem mkOfCompact_neg [CompactSpace Î±] (f : C(Î±, Î²)) : mkOfCompact (-f) = -mkOfCompact f := rfl

@[simp]
theorem mkOfCompact_sub [CompactSpace Î±] (f g : C(Î±, Î²)) :
    mkOfCompact (f - g) = mkOfCompact f - mkOfCompact g := rfl

@[simp]
theorem coe_zsmulRec : âˆ€ z, â‡‘(zsmulRec (Â· â€¢ Â·) z f) = z â€¢ â‡‘f
  | Int.ofNat n => by rw [zsmulRec, Int.ofNat_eq_coe, coe_nsmul, natCast_zsmul]
  | Int.negSucc n => by rw [zsmulRec, negSucc_zsmul, coe_neg, coe_nsmul]

instance instSMulInt : SMul â„¤ (Î± â†’áµ‡ Î²) where
  smul n f :=
    { toContinuousMap := n â€¢ f.toContinuousMap
      map_bounded' := by simpa using (zsmulRec (Â· â€¢ Â·) n f).map_bounded' }

@[simp]
theorem coe_zsmul (r : â„¤) (f : Î± â†’áµ‡ Î²) : â‡‘(r â€¢ f) = r â€¢ â‡‘f := rfl

@[simp]
theorem zsmul_apply (r : â„¤) (f : Î± â†’áµ‡ Î²) (v : Î±) : (r â€¢ f) v = r â€¢ f v := rfl

instance instAddCommGroup : AddCommGroup (Î± â†’áµ‡ Î²) :=
  DFunLike.coe_injective.addCommGroup _ coe_zero coe_add coe_neg coe_sub (fun _ _ â†¦ coe_nsmul _ _)
    fun _ _ â†¦ coe_zsmul _ _

instance instSeminormedAddCommGroup : SeminormedAddCommGroup (Î± â†’áµ‡ Î²) where
  dist_eq f g := by simp only [norm_eq, dist_eq, dist_eq_norm, sub_apply]

instance instNormedAddCommGroup {Î± Î²} [TopologicalSpace Î±] [NormedAddCommGroup Î²] :
    NormedAddCommGroup (Î± â†’áµ‡ Î²) :=
  { instSeminormedAddCommGroup with
    eq_of_dist_eq_zero }

theorem nnnorm_def : â€–fâ€–â‚Š = nndist f 0 := rfl

theorem nnnorm_coe_le_nnnorm (x : Î±) : â€–f xâ€–â‚Š â‰¤ â€–fâ€–â‚Š :=
  norm_coe_le_norm _ _

theorem nndist_le_two_nnnorm (x y : Î±) : nndist (f x) (f y) â‰¤ 2 * â€–fâ€–â‚Š :=
  dist_le_two_norm _ _ _

/-- The `nnnorm` of a function is controlled by the supremum of the pointwise `nnnorm`s. -/
theorem nnnorm_le (C : â„â‰¥0) : â€–fâ€–â‚Š â‰¤ C â†” âˆ€ x : Î±, â€–f xâ€–â‚Š â‰¤ C :=
  norm_le C.prop

theorem nnnorm_const_le (b : Î²) : â€–const Î± bâ€–â‚Š â‰¤ â€–bâ€–â‚Š :=
  norm_const_le _

@[simp]
theorem nnnorm_const_eq [Nonempty Î±] (b : Î²) : â€–const Î± bâ€–â‚Š = â€–bâ€–â‚Š :=
  Subtype.ext <| norm_const_eq _

theorem nnnorm_eq_iSup_nnnorm : â€–fâ€–â‚Š = â¨† x : Î±, â€–f xâ€–â‚Š :=
  Subtype.ext <| (norm_eq_iSup_norm f).trans <| by simp_rw [val_eq_coe, NNReal.coe_iSup, coe_nnnorm]

theorem enorm_eq_iSup_enorm : â€–fâ€–â‚‘ = â¨† x, â€–f xâ€–â‚‘ := by
  simpa only [â† edist_zero_eq_enorm] using edist_eq_iSup

theorem abs_diff_coe_le_dist : â€–f x - g xâ€– â‰¤ dist f g := by
  rw [dist_eq_norm]
  exact (f - g).norm_coe_le_norm x

theorem coe_le_coe_add_dist {f g : Î± â†’áµ‡ â„} : f x â‰¤ g x + dist f g :=
  sub_le_iff_le_add'.1 <| (abs_le.1 <| @dist_coe_le_dist _ _ _ _ f g x).2

theorem norm_compContinuous_le [TopologicalSpace Î³] (f : Î± â†’áµ‡ Î²) (g : C(Î³, Î±)) :
    â€–f.compContinuous gâ€– â‰¤ â€–fâ€– :=
  ((lipschitz_compContinuous g).dist_le_mul f 0).trans <| by
    rw [NNReal.coe_one, one_mul, dist_zero_right]

end NormedAddCommGroup

section NormedSpace

variable {ğ•œ : Type*}
variable [TopologicalSpace Î±] [SeminormedAddCommGroup Î²]
variable {f g : Î± â†’áµ‡ Î²} {x : Î±} {C : â„}

instance instNormedSpace [NormedField ğ•œ] [NormedSpace ğ•œ Î²] : NormedSpace ğ•œ (Î± â†’áµ‡ Î²) :=
  âŸ¨fun c f â†¦ by
    refine norm_ofNormedAddCommGroup_le _ (mul_nonneg (norm_nonneg _) (norm_nonneg _)) ?_
    exact fun x â†¦
      norm_smul c (f x) â–¸ mul_le_mul_of_nonneg_left (f.norm_coe_le_norm _) (norm_nonneg _)âŸ©

variable [NontriviallyNormedField ğ•œ] [NormedSpace ğ•œ Î²]
variable [SeminormedAddCommGroup Î³] [NormedSpace ğ•œ Î³]
variable (Î±)

-- TODO does this work in the `IsBoundedSMul` setting, too?
/-- Postcomposition of bounded continuous functions into a normed module by a continuous linear map
is a continuous linear map.
Upgraded version of `ContinuousLinearMap.compLeftContinuous`, similar to `LinearMap.compLeft`. -/
protected def _root_.ContinuousLinearMap.compLeftContinuousBounded (g : Î² â†’L[ğ•œ] Î³) :
    (Î± â†’áµ‡ Î²) â†’L[ğ•œ] Î± â†’áµ‡ Î³ :=
  LinearMap.mkContinuous
    { toFun := fun f â†¦
        ofNormedAddCommGroup (g âˆ˜ f) (g.continuous.comp f.continuous) (â€–gâ€– * â€–fâ€–) fun x â†¦
          g.le_opNorm_of_le (f.norm_coe_le_norm x)
      map_add' := fun f g â†¦ by ext; simp
      map_smul' := fun c f â†¦ by ext; simp } â€–gâ€– fun f â†¦
        norm_ofNormedAddCommGroup_le _ (mul_nonneg (norm_nonneg g) (norm_nonneg f))
          (fun x â†¦ by exact g.le_opNorm_of_le (f.norm_coe_le_norm x))

@[simp]
theorem _root_.ContinuousLinearMap.compLeftContinuousBounded_apply (g : Î² â†’L[ğ•œ] Î³) (f : Î± â†’áµ‡ Î²)
    (x : Î±) : (g.compLeftContinuousBounded Î± f) x = g (f x) := rfl

end NormedSpace

section NormedRing

variable [TopologicalSpace Î±] {R : Type*}

section NonUnital

section Seminormed

variable [NonUnitalSeminormedRing R]

instance instNonUnitalRing : NonUnitalRing (Î± â†’áµ‡ R) :=
  DFunLike.coe_injective.nonUnitalRing _ coe_zero coe_add coe_mul coe_neg coe_sub
    (fun _ _ â†¦ coe_nsmul _ _) fun _ _ â†¦ coe_zsmul _ _

instance instNonUnitalSeminormedRing : NonUnitalSeminormedRing (Î± â†’áµ‡ R) where
  __ := instSeminormedAddCommGroup
  __ := instNonUnitalRing
  norm_mul_le f g := norm_ofNormedAddCommGroup_le _ (by positivity)
    (fun x â†¦ (norm_mul_le _ _).trans <| mul_le_mul
      (norm_coe_le_norm f x) (norm_coe_le_norm g x) (norm_nonneg _) (norm_nonneg _))

end Seminormed

instance instNonUnitalSeminormedCommRing [NonUnitalSeminormedCommRing R] :
    NonUnitalSeminormedCommRing (Î± â†’áµ‡ R) where
  mul_comm _ _ := ext fun _ â†¦ mul_comm ..

instance instNonUnitalNormedRing [NonUnitalNormedRing R] : NonUnitalNormedRing (Î± â†’áµ‡ R) where
  __ := instNonUnitalSeminormedRing
  __ := instNormedAddCommGroup

instance instNonUnitalNormedCommRing [NonUnitalNormedCommRing R] :
    NonUnitalNormedCommRing (Î± â†’áµ‡ R) where
  mul_comm := mul_comm

end NonUnital

section Seminormed

variable [SeminormedRing R]

@[simp]
theorem coe_npowRec (f : Î± â†’áµ‡ R) : âˆ€ n, â‡‘(npowRec n f) = (â‡‘f) ^ n
  | 0 => by rw [npowRec, pow_zero, coe_one]
  | n + 1 => by rw [npowRec, pow_succ, coe_mul, coe_npowRec f n]

instance hasNatPow : Pow (Î± â†’áµ‡ R) â„• where
  pow f n :=
    { toContinuousMap := f.toContinuousMap ^ n
      map_bounded' := by simpa [coe_npowRec] using (npowRec n f).map_bounded' }

instance : NatCast (Î± â†’áµ‡ R) :=
  âŸ¨fun n â†¦ BoundedContinuousFunction.const _ nâŸ©

@[simp, norm_cast]
theorem coe_natCast (n : â„•) : ((n : Î± â†’áµ‡ R) : Î± â†’ R) = n := rfl

@[simp, norm_cast]
theorem coe_ofNat (n : â„•) [n.AtLeastTwo] :
    ((ofNat(n) : Î± â†’áµ‡ R) : Î± â†’ R) = ofNat(n) :=
  rfl

instance : IntCast (Î± â†’áµ‡ R) :=
  âŸ¨fun n â†¦ BoundedContinuousFunction.const _ nâŸ©

@[simp, norm_cast]
theorem coe_intCast (n : â„¤) : ((n : Î± â†’áµ‡ R) : Î± â†’ R) = n := rfl

instance instRing : Ring (Î± â†’áµ‡ R) :=
  DFunLike.coe_injective.ring _ coe_zero coe_one coe_add coe_mul coe_neg coe_sub
    (fun _ _ â†¦ coe_nsmul _ _) (fun _ _ â†¦ coe_zsmul _ _) (fun _ _ â†¦ coe_pow _ _) coe_natCast
    coe_intCast

instance instSeminormedRing : SeminormedRing (Î± â†’áµ‡ R) where
  __ := instRing
  __ := instNonUnitalSeminormedRing

/-- Composition on the left by a (lipschitz-continuous) homomorphism of topological semirings, as a
`RingHom`. Similar to `RingHom.compLeftContinuous`. -/
@[simps!]
protected def _root_.RingHom.compLeftContinuousBounded (Î± : Type*)
    [TopologicalSpace Î±] [SeminormedRing Î²] [SeminormedRing Î³]
    (g : Î² â†’+* Î³) {C : NNReal} (hg : LipschitzWith C g) : (Î± â†’áµ‡ Î²) â†’+* (Î± â†’áµ‡ Î³) :=
  { g.toMonoidHom.compLeftContinuousBounded Î± hg,
    g.toAddMonoidHom.compLeftContinuousBounded Î± hg with }

end Seminormed

instance instNormedRing [NormedRing R] : NormedRing (Î± â†’áµ‡ R) where
  __ := instRing
  __ := instNonUnitalNormedRing

end NormedRing

section NormedCommRing

variable [TopologicalSpace Î±] {R : Type*}

instance instCommRing [SeminormedCommRing R] : CommRing (Î± â†’áµ‡ R) where
  mul_comm _ _ := ext fun _ â†¦ mul_comm _ _

instance instSeminormedCommRing [SeminormedCommRing R] : SeminormedCommRing (Î± â†’áµ‡ R) where
  __ := instCommRing
  __ := instNonUnitalSeminormedRing

instance instNormedCommRing [NormedCommRing R] : NormedCommRing (Î± â†’áµ‡ R) where
  __ := instSeminormedCommRing
  __ := instNormedAddCommGroup

end NormedCommRing

section NonUnitalAlgebra

-- these hypotheses could be generalized if we generalize `IsBoundedSMul` to `Bornology`.
variable {ğ•œ : Type*} [PseudoMetricSpace ğ•œ] [TopologicalSpace Î±] [NonUnitalSeminormedRing Î²]
variable [Zero ğ•œ] [SMul ğ•œ Î²] [IsBoundedSMul ğ•œ Î²]

instance [IsScalarTower ğ•œ Î² Î²] : IsScalarTower ğ•œ (Î± â†’áµ‡ Î²) (Î± â†’áµ‡ Î²) where
  smul_assoc _ _ _ := ext fun _ â†¦ smul_mul_assoc ..

instance [SMulCommClass ğ•œ Î² Î²] : SMulCommClass ğ•œ (Î± â†’áµ‡ Î²) (Î± â†’áµ‡ Î²) where
  smul_comm _ _ _ := ext fun _ â†¦ (mul_smul_comm ..).symm

instance [SMulCommClass ğ•œ Î² Î²] : SMulCommClass (Î± â†’áµ‡ Î²) ğ•œ (Î± â†’áµ‡ Î²) where
  smul_comm _ _ _ := ext fun _ â†¦ mul_smul_comm ..

end NonUnitalAlgebra

section NormedAlgebra

variable {ğ•œ : Type*} [NormedField ğ•œ]
variable [TopologicalSpace Î±] [SeminormedAddCommGroup Î²] [NormedSpace ğ•œ Î²]
variable [NormedRing Î³] [NormedAlgebra ğ•œ Î³]
variable {f g : Î± â†’áµ‡ Î³} {x : Î±} {c : ğ•œ}

/-- `BoundedContinuousFunction.const` as a `RingHom`. -/
def C : ğ•œ â†’+* Î± â†’áµ‡ Î³ where
  toFun := fun c : ğ•œ â†¦ const Î± ((algebraMap ğ•œ Î³) c)
  map_one' := ext fun _ â†¦ (algebraMap ğ•œ Î³).map_one
  map_mul' _ _ := ext fun _ â†¦ (algebraMap ğ•œ Î³).map_mul _ _
  map_zero' := ext fun _ â†¦ (algebraMap ğ•œ Î³).map_zero
  map_add' _ _ := ext fun _ â†¦ (algebraMap ğ•œ Î³).map_add _ _

instance instAlgebra : Algebra ğ•œ (Î± â†’áµ‡ Î³) where
  algebraMap := C
  commutes' _ _ := ext fun _ â†¦ Algebra.commutes' _ _
  smul_def' _ _ := ext fun _ â†¦ Algebra.smul_def' _ _

@[simp]
theorem algebraMap_apply (k : ğ•œ) (a : Î±) : algebraMap ğ•œ (Î± â†’áµ‡ Î³) k a = k â€¢ (1 : Î³) := by
  simp only [Algebra.algebraMap_eq_smul_one, coe_smul, coe_one, Pi.one_apply]

instance instNormedAlgebra : NormedAlgebra ğ•œ (Î± â†’áµ‡ Î³) where
  __ := instAlgebra
  __ := instNormedSpace

variable (ğ•œ)

/-- Composition on the left by a (lipschitz-continuous) homomorphism of topological `R`-algebras,
as an `AlgHom`. Similar to `AlgHom.compLeftContinuous`. -/
@[simps!]
protected def AlgHom.compLeftContinuousBounded
    [NormedRing Î²] [NormedAlgebra ğ•œ Î²] [NormedRing Î³] [NormedAlgebra ğ•œ Î³]
    (g : Î² â†’â‚[ğ•œ] Î³) {C : NNReal} (hg : LipschitzWith C g) : (Î± â†’áµ‡ Î²) â†’â‚[ğ•œ] (Î± â†’áµ‡ Î³) :=
  { g.toRingHom.compLeftContinuousBounded Î± hg with
    commutes' := fun _ â†¦ DFunLike.ext _ _ fun _ â†¦ g.commutes' _ }

/-- The algebra-homomorphism forgetting that a bounded continuous function is bounded. -/
@[simps]
def toContinuousMapâ‚ : (Î± â†’áµ‡ Î³) â†’â‚[ğ•œ] C(Î±, Î³) where
  toFun := (â†‘)
  map_one' := rfl
  map_mul' _ _ := rfl
  map_zero' := rfl
  map_add' _ _ := rfl
  commutes' _ := rfl

@[simp]
theorem coe_toContinuousMapâ‚ (f : Î± â†’áµ‡ Î³) : (f.toContinuousMapâ‚ ğ•œ : Î± â†’ Î³) = f := rfl

variable {ğ•œ}

/-! ### Structure as normed module over scalar functions

If `Î²` is a normed `ğ•œ`-space, then we show that the space of bounded continuous
functions from `Î±` to `Î²` is naturally a module over the algebra of bounded continuous
functions from `Î±` to `ğ•œ`. -/

instance instSMul' : SMul (Î± â†’áµ‡ ğ•œ) (Î± â†’áµ‡ Î²) where
  smul f g :=
    ofNormedAddCommGroup (fun x â†¦ f x â€¢ g x) (f.continuous.smul g.continuous) (â€–fâ€– * â€–gâ€–) fun x â†¦
      calc
        â€–f x â€¢ g xâ€– â‰¤ â€–f xâ€– * â€–g xâ€– := norm_smul_le _ _
        _ â‰¤ â€–fâ€– * â€–gâ€– :=
          mul_le_mul (f.norm_coe_le_norm _) (g.norm_coe_le_norm _) (norm_nonneg _) (norm_nonneg _)

instance instModule' : Module (Î± â†’áµ‡ ğ•œ) (Î± â†’áµ‡ Î²) :=
  Module.ofMinimalAxioms
      (fun c _ _ â†¦ ext fun a â†¦ smul_add (c a) _ _)
      (fun _ _ _ â†¦ ext fun _ â†¦ add_smul _ _ _)
      (fun _ _ _ â†¦ ext fun _ â†¦ mul_smul _ _ _)
      (fun f â†¦ ext fun x â†¦ one_smul ğ•œ (f x))

/- TODO: When `NormedModule` has been added to `Analysis.Normed.Module.Basic`, this
shows that the space of bounded continuous functions from `Î±` to `Î²` is naturally a normed
module over the algebra of bounded continuous functions from `Î±` to `ğ•œ`. -/
instance : IsBoundedSMul (Î± â†’áµ‡ ğ•œ) (Î± â†’áµ‡ Î²) :=
  IsBoundedSMul.of_norm_smul_le fun _ _ â†¦
    norm_ofNormedAddCommGroup_le _ (mul_nonneg (norm_nonneg _) (norm_nonneg _)) _

end NormedAlgebra

section NormedLatticeOrderedGroup

variable [TopologicalSpace Î±]
  [NormedAddCommGroup Î²] [Lattice Î²] [HasSolidNorm Î²] [IsOrderedAddMonoid Î²]

instance instPartialOrder : PartialOrder (Î± â†’áµ‡ Î²) :=
  PartialOrder.lift (fun f â†¦ f.toFun) (by simp [Injective])

instance instSup : Max (Î± â†’áµ‡ Î²) where
  max f g :=
    { toFun := f âŠ” g
      continuous_toFun := f.continuous.sup g.continuous
      map_bounded' := by
        obtain âŸ¨Câ‚, hfâŸ© := f.bounded
        obtain âŸ¨Câ‚‚, hgâŸ© := g.bounded
        refine âŸ¨Câ‚ + Câ‚‚, fun x y â†¦ ?_âŸ©
        simp_rw [dist_eq_norm_sub] at hf hg âŠ¢
        exact (norm_sup_sub_sup_le_add_norm _ _ _ _).trans (add_le_add (hf _ _) (hg _ _)) }

instance instInf : Min (Î± â†’áµ‡ Î²) where
  min f g :=
    { toFun := f âŠ“ g
      continuous_toFun := f.continuous.inf g.continuous
      map_bounded' := by
        obtain âŸ¨Câ‚, hfâŸ© := f.bounded
        obtain âŸ¨Câ‚‚, hgâŸ© := g.bounded
        refine âŸ¨Câ‚ + Câ‚‚, fun x y â†¦ ?_âŸ©
        simp_rw [dist_eq_norm_sub] at hf hg âŠ¢
        exact (norm_inf_sub_inf_le_add_norm _ _ _ _).trans (add_le_add (hf _ _) (hg _ _)) }

@[simp, norm_cast] lemma coe_sup (f g : Î± â†’áµ‡ Î²) : â‡‘(f âŠ” g) = â‡‘f âŠ” â‡‘g := rfl

@[simp, norm_cast] lemma coe_inf (f g : Î± â†’áµ‡ Î²) : â‡‘(f âŠ“ g) = â‡‘f âŠ“ â‡‘g := rfl

instance instSemilatticeSup : SemilatticeSup (Î± â†’áµ‡ Î²) :=
  DFunLike.coe_injective.semilatticeSup _ coe_sup

instance instSemilatticeInf : SemilatticeInf (Î± â†’áµ‡ Î²) :=
  DFunLike.coe_injective.semilatticeInf _ coe_inf

instance instLattice : Lattice (Î± â†’áµ‡ Î²) := DFunLike.coe_injective.lattice _ coe_sup coe_inf

@[simp, norm_cast] lemma coe_abs (f : Î± â†’áµ‡ Î²) : â‡‘|f| = |â‡‘f| := rfl
@[simp, norm_cast] lemma coe_posPart (f : Î± â†’áµ‡ Î²) : â‡‘fâº = (â‡‘f)âº := rfl
@[simp, norm_cast] lemma coe_negPart (f : Î± â†’áµ‡ Î²) : â‡‘fâ» = (â‡‘f)â» := rfl

instance instHasSolidNorm : HasSolidNorm (Î± â†’áµ‡ Î²) :=
  { solid := by
      intro f g h
      have i1 : âˆ€ t, â€–f tâ€– â‰¤ â€–g tâ€– := fun t â†¦ HasSolidNorm.solid (h t)
      rw [norm_le (norm_nonneg _)]
      exact fun t â†¦ (i1 t).trans (norm_coe_le_norm g t) }

instance instIsOrderedAddMonoid : IsOrderedAddMonoid (Î± â†’áµ‡ Î²) :=
  { add_le_add_left := by
      intro f g hâ‚ h t
      simp only [ContinuousMap.toFun_eq_coe, coe_toContinuousMap, coe_add, Pi.add_apply,
        add_le_add_iff_left]
      exact hâ‚ _ }

end NormedLatticeOrderedGroup

section NonnegativePart

variable [TopologicalSpace Î±]

/-- The nonnegative part of a bounded continuous `â„`-valued function as a bounded
continuous `â„â‰¥0`-valued function. -/
def nnrealPart (f : Î± â†’áµ‡ â„) : Î± â†’áµ‡ â„â‰¥0 :=
  BoundedContinuousFunction.comp _ (show LipschitzWith 1 Real.toNNReal from lipschitzWith_posPart) f

@[simp]
theorem nnrealPart_coeFn_eq (f : Î± â†’áµ‡ â„) : â‡‘f.nnrealPart = Real.toNNReal âˆ˜ â‡‘f := rfl

/-- The absolute value of a bounded continuous `â„`-valued function as a bounded
continuous `â„â‰¥0`-valued function. -/
def nnnorm (f : Î± â†’áµ‡ â„) : Î± â†’áµ‡ â„â‰¥0 :=
  BoundedContinuousFunction.comp _
    (show LipschitzWith 1 fun x : â„ â†¦ â€–xâ€–â‚Š from lipschitzWith_one_norm) f

@[simp]
theorem nnnorm_coeFn_eq (f : Î± â†’áµ‡ â„) : â‡‘f.nnnorm = NNNorm.nnnorm âˆ˜ â‡‘f := rfl

-- TODO: Use `posPart` and `negPart` here
/-- Decompose a bounded continuous function to its positive and negative parts. -/
theorem self_eq_nnrealPart_sub_nnrealPart_neg (f : Î± â†’áµ‡ â„) :
    â‡‘f = (â†‘) âˆ˜ f.nnrealPart - (â†‘) âˆ˜ (-f).nnrealPart := by
  funext x
  dsimp
  simp only [max_zero_sub_max_neg_zero_eq_self]

/-- Express the absolute value of a bounded continuous function in terms of its
positive and negative parts. -/
theorem abs_self_eq_nnrealPart_add_nnrealPart_neg (f : Î± â†’áµ‡ â„) :
    abs âˆ˜ â‡‘f = (â†‘) âˆ˜ f.nnrealPart + (â†‘) âˆ˜ (-f).nnrealPart := by
  funext x
  dsimp
  simp only [max_zero_add_max_neg_zero_eq_abs_self]

end NonnegativePart

section

variable {Î± : Type*} [TopologicalSpace Î±]

-- TODO: `f + const _ â€–fâ€–` is just `fâº`
lemma add_norm_nonneg (f : Î± â†’áµ‡ â„) :
    0 â‰¤ f + const _ â€–fâ€– := by
  intro x
  simp only [ContinuousMap.toFun_eq_coe, coe_toContinuousMap, coe_zero, Pi.zero_apply, coe_add,
    const_apply, Pi.add_apply]
  linarith [(abs_le.mp (norm_coe_le_norm f x)).1]

lemma norm_sub_nonneg (f : Î± â†’áµ‡ â„) :
    0 â‰¤ const _ â€–fâ€– - f := by
  intro x
  simp only [ContinuousMap.toFun_eq_coe, coe_toContinuousMap, coe_zero, Pi.zero_apply, coe_sub,
    const_apply, Pi.sub_apply, sub_nonneg]
  linarith [(abs_le.mp (norm_coe_le_norm f x)).2]

end

end BoundedContinuousFunction
