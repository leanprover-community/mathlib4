/-
Copyright (c) 2020 Nicol√≤ Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Nicol√≤ Cavalleri
-/
import Mathlib.Data.Set.UnionLift
import Mathlib.Topology.ContinuousMap.Defs
import Mathlib.Topology.Homeomorph.Defs
import Mathlib.Topology.Separation.Hausdorff

/-!
# Continuous bundled maps

In this file we define the type `ContinuousMap` of continuous bundled maps.

We use the `DFunLike` design, so each type of morphisms has a companion typeclass which is meant to
be satisfied by itself and all stricter types.
-/


open Function Topology

section ContinuousMapClass

variable {F Œ± Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [FunLike F Œ± Œ≤]
variable [ContinuousMapClass F Œ± Œ≤]

theorem map_continuousAt (f : F) (a : Œ±) : ContinuousAt f a :=
  (map_continuous f).continuousAt

theorem map_continuousWithinAt (f : F) (s : Set Œ±) (a : Œ±) : ContinuousWithinAt f s a :=
  (map_continuous f).continuousWithinAt

end ContinuousMapClass

/-! ### Continuous maps -/


namespace ContinuousMap

variable {Œ± Œ≤ Œ≥ Œ¥ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [TopologicalSpace Œ≥]
  [TopologicalSpace Œ¥]

variable {f g : C(Œ±, Œ≤)}

/-- Deprecated. Use `map_continuousAt` instead. -/
protected theorem continuousAt (f : C(Œ±, Œ≤)) (x : Œ±) : ContinuousAt f x :=
  map_continuousAt f x

theorem map_specializes (f : C(Œ±, Œ≤)) {x y : Œ±} (h : x ‚§≥ y) : f x ‚§≥ f y :=
  h.map f.2

section DiscreteTopology
variable [DiscreteTopology Œ±]

/--
The continuous functions from `Œ±` to `Œ≤` are the same as the plain functions when `Œ±` is discrete.
-/
@[simps]
def equivFnOfDiscrete : C(Œ±, Œ≤) ‚âÉ (Œ± ‚Üí Œ≤) :=
  ‚ü®fun f ‚Ü¶ f,
    fun f ‚Ü¶ ‚ü®f, continuous_of_discreteTopology‚ü©,
    fun _ ‚Ü¶ by ext; rfl,
    fun _ ‚Ü¶ by ext; rfl‚ü©

@[simp] lemma coe_equivFnOfDiscrete : ‚áëequivFnOfDiscrete = (DFunLike.coe : C(Œ±, Œ≤) ‚Üí Œ± ‚Üí Œ≤) := rfl

@[simp] lemma equivFnOfDiscrete_symm_apply (f : Œ± ‚Üí Œ≤) : equivFnOfDiscrete.symm f = f := rfl

end DiscreteTopology

variable (Œ±)

/-- The identity as a continuous map. -/
protected def id : C(Œ±, Œ±) where
  toFun := id

@[simp, norm_cast]
theorem coe_id : ‚áë(ContinuousMap.id Œ±) = id :=
  rfl

/-- The constant map as a continuous map. -/
def const (b : Œ≤) : C(Œ±, Œ≤) where
  toFun := fun _ : Œ± ‚Ü¶ b

@[simp]
theorem coe_const (b : Œ≤) : ‚áë(const Œ± b) = Function.const Œ± b :=
  rfl

/-- `Function.const Œ± b` as a bundled continuous function of `b`. -/
@[simps -fullyApplied]
def constPi : C(Œ≤, Œ± ‚Üí Œ≤) where
  toFun b := Function.const Œ± b

instance [Inhabited Œ≤] : Inhabited C(Œ±, Œ≤) :=
  ‚ü®const Œ± default‚ü©

variable {Œ±}

@[simp]
theorem id_apply (a : Œ±) : ContinuousMap.id Œ± a = a :=
  rfl

@[simp]
theorem const_apply (b : Œ≤) (a : Œ±) : const Œ± b a = b :=
  rfl

/-- The composition of continuous maps, as a continuous map. -/
def comp (f : C(Œ≤, Œ≥)) (g : C(Œ±, Œ≤)) : C(Œ±, Œ≥) where
  toFun := f ‚àò g

@[simp]
theorem coe_comp (f : C(Œ≤, Œ≥)) (g : C(Œ±, Œ≤)) : ‚áë(comp f g) = f ‚àò g :=
  rfl

@[simp]
theorem comp_apply (f : C(Œ≤, Œ≥)) (g : C(Œ±, Œ≤)) (a : Œ±) : comp f g a = f (g a) :=
  rfl

@[simp]
theorem comp_assoc (f : C(Œ≥, Œ¥)) (g : C(Œ≤, Œ≥)) (h : C(Œ±, Œ≤)) :
    (f.comp g).comp h = f.comp (g.comp h) :=
  rfl

@[simp]
theorem id_comp (f : C(Œ±, Œ≤)) : (ContinuousMap.id _).comp f = f :=
  ext fun _ ‚Ü¶ rfl

@[simp]
theorem comp_id (f : C(Œ±, Œ≤)) : f.comp (ContinuousMap.id _) = f :=
  ext fun _ ‚Ü¶ rfl

@[simp]
theorem const_comp (c : Œ≥) (f : C(Œ±, Œ≤)) : (const Œ≤ c).comp f = const Œ± c :=
  ext fun _ ‚Ü¶ rfl

@[simp]
theorem comp_const (f : C(Œ≤, Œ≥)) (b : Œ≤) : f.comp (const Œ± b) = const Œ± (f b) :=
  ext fun _ ‚Ü¶ rfl

@[simp]
theorem cancel_right {f‚ÇÅ f‚ÇÇ : C(Œ≤, Œ≥)} {g : C(Œ±, Œ≤)} (hg : Surjective g) :
    f‚ÇÅ.comp g = f‚ÇÇ.comp g ‚Üî f‚ÇÅ = f‚ÇÇ :=
  ‚ü®fun h ‚Ü¶ ext <| hg.forall.2 <| DFunLike.ext_iff.1 h, congr_arg (ContinuousMap.comp ¬∑ g)‚ü©

@[simp]
theorem cancel_left {f : C(Œ≤, Œ≥)} {g‚ÇÅ g‚ÇÇ : C(Œ±, Œ≤)} (hf : Injective f) :
    f.comp g‚ÇÅ = f.comp g‚ÇÇ ‚Üî g‚ÇÅ = g‚ÇÇ :=
  ‚ü®fun h ‚Ü¶ ext fun a ‚Ü¶ hf <| by rw [‚Üê comp_apply, h, comp_apply], congr_arg _‚ü©

instance [Nonempty Œ±] [Nontrivial Œ≤] : Nontrivial C(Œ±, Œ≤) :=
  ‚ü®let ‚ü®b‚ÇÅ, b‚ÇÇ, hb‚ü© := exists_pair_ne Œ≤
  ‚ü®const _ b‚ÇÅ, const _ b‚ÇÇ, fun h ‚Ü¶ hb <| DFunLike.congr_fun h <| Classical.arbitrary Œ±‚ü©‚ü©

/-- The bijection `C(X‚ÇÅ, Y‚ÇÅ) ‚âÉ C(X‚ÇÇ, Y‚ÇÇ)` induced by homeomorphisms
`e : X‚ÇÅ ‚âÉ‚Çú X‚ÇÇ` and `e' : Y‚ÇÅ ‚âÉ‚Çú Y‚ÇÇ`. -/
@[simps]
def _root_.Homeomorph.continuousMapCongr {X‚ÇÅ X‚ÇÇ Y‚ÇÅ Y‚ÇÇ : Type*}
    [TopologicalSpace X‚ÇÅ] [TopologicalSpace X‚ÇÇ]
    [TopologicalSpace Y‚ÇÅ] [TopologicalSpace Y‚ÇÇ]
    (e : X‚ÇÅ ‚âÉ‚Çú X‚ÇÇ) (e' : Y‚ÇÅ ‚âÉ‚Çú Y‚ÇÇ) :
    C(X‚ÇÅ, Y‚ÇÅ) ‚âÉ C(X‚ÇÇ, Y‚ÇÇ) where
  toFun f := ContinuousMap.comp ‚ü®_, e'.continuous‚ü© (f.comp ‚ü®_, e.symm.continuous‚ü©)
  invFun g := ContinuousMap.comp ‚ü®_, e'.symm.continuous‚ü© (g.comp ‚ü®_, e.continuous‚ü©)
  left_inv _ := by aesop
  right_inv _ := by aesop

section Prod

variable {Œ±‚ÇÅ Œ±‚ÇÇ Œ≤‚ÇÅ Œ≤‚ÇÇ : Type*} [TopologicalSpace Œ±‚ÇÅ] [TopologicalSpace Œ±‚ÇÇ] [TopologicalSpace Œ≤‚ÇÅ]
  [TopologicalSpace Œ≤‚ÇÇ]

/-- `Prod.fst : (x, y) ‚Ü¶ x` as a bundled continuous map. -/
@[simps -fullyApplied]
def fst : C(Œ± √ó Œ≤, Œ±) where
  toFun := Prod.fst

/-- `Prod.snd : (x, y) ‚Ü¶ y` as a bundled continuous map. -/
@[simps -fullyApplied]
def snd : C(Œ± √ó Œ≤, Œ≤) where
  toFun := Prod.snd

/-- Given two continuous maps `f` and `g`, this is the continuous map `x ‚Ü¶ (f x, g x)`. -/
def prodMk (f : C(Œ±, Œ≤‚ÇÅ)) (g : C(Œ±, Œ≤‚ÇÇ)) : C(Œ±, Œ≤‚ÇÅ √ó Œ≤‚ÇÇ) where
  toFun x := (f x, g x)

/-- Given two continuous maps `f` and `g`, this is the continuous map `(x, y) ‚Ü¶ (f x, g y)`. -/
@[simps]
def prodMap (f : C(Œ±‚ÇÅ, Œ±‚ÇÇ)) (g : C(Œ≤‚ÇÅ, Œ≤‚ÇÇ)) : C(Œ±‚ÇÅ √ó Œ≤‚ÇÅ, Œ±‚ÇÇ √ó Œ≤‚ÇÇ) where
  toFun := Prod.map f g

@[simp]
theorem prod_eval (f : C(Œ±, Œ≤‚ÇÅ)) (g : C(Œ±, Œ≤‚ÇÇ)) (a : Œ±) : (prodMk f g) a = (f a, g a) :=
  rfl

/-- `Prod.swap` bundled as a `ContinuousMap`. -/
@[simps!]
def prodSwap : C(Œ± √ó Œ≤, Œ≤ √ó Œ±) := .prodMk .snd .fst

end Prod

section Sigma

variable {I A : Type*} {X : I ‚Üí Type*} [TopologicalSpace A] [‚àÄ i, TopologicalSpace (X i)]

/-- `Sigma.mk i` as a bundled continuous map. -/
@[simps apply]
def sigmaMk (i : I) : C(X i, Œ£ i, X i) where
  toFun := Sigma.mk i

/--
To give a continuous map out of a disjoint union, it suffices to give a continuous map out of
each term. This is `Sigma.uncurry` for continuous maps.
-/
@[simps]
def sigma (f : ‚àÄ i, C(X i, A)) : C((Œ£ i, X i), A) where
  toFun ig := f ig.fst ig.snd

variable (A X) in
/--
Giving a continuous map out of a disjoint union is the same as giving a continuous map out of
each term. This is a version of `Equiv.piCurry` for continuous maps.
-/
@[simps]
def sigmaEquiv : (‚àÄ i, C(X i, A)) ‚âÉ C((Œ£ i, X i), A) where
  toFun := sigma
  invFun f i := f.comp (sigmaMk i)

end Sigma

section Pi

variable {I A : Type*} {X Y : I ‚Üí Type*} [TopologicalSpace A] [‚àÄ i, TopologicalSpace (X i)]
  [‚àÄ i, TopologicalSpace (Y i)]

/-- Abbreviation for product of continuous maps, which is continuous -/
def pi (f : ‚àÄ i, C(A, X i)) : C(A, ‚àÄ i, X i) where
  toFun (a : A) (i : I) := f i a

@[simp]
theorem pi_eval (f : ‚àÄ i, C(A, X i)) (a : A) : (pi f) a = fun i : I ‚Ü¶ (f i) a :=
  rfl

/-- Evaluation at point as a bundled continuous map. -/
@[simps -fullyApplied]
def eval (i : I) : C(‚àÄ j, X j, X i) where
  toFun := Function.eval i

variable (A X) in
/--
Giving a continuous map out of a disjoint union is the same as giving a continuous map out of
each term
-/
@[simps]
def piEquiv : (‚àÄ i, C(A, X i)) ‚âÉ C(A, ‚àÄ i, X i) where
  toFun := pi
  invFun f i := (eval i).comp f

/-- Combine a collection of bundled continuous maps `C(X i, Y i)` into a bundled continuous map
`C(‚àÄ i, X i, ‚àÄ i, Y i)`. -/
@[simps!]
def piMap (f : ‚àÄ i, C(X i, Y i)) : C((i : I) ‚Üí X i, (i : I) ‚Üí Y i) :=
  .pi fun i ‚Ü¶ (f i).comp (eval i)

/-- "Precomposition" as a continuous map between dependent types. -/
def precomp {Œπ : Type*} (œÜ : Œπ ‚Üí I) : C((i : I) ‚Üí X i, (i : Œπ) ‚Üí X (œÜ i)) :=
  ‚ü®_, Pi.continuous_precomp' œÜ‚ü©

end Pi

section Restrict

variable (s : Set Œ±)

/-- The restriction of a continuous function `Œ± ‚Üí Œ≤` to a subset `s` of `Œ±`. -/
def restrict (f : C(Œ±, Œ≤)) : C(s, Œ≤) where
  toFun := f ‚àò ((‚Üë) : s ‚Üí Œ±)

@[simp]
theorem coe_restrict (f : C(Œ±, Œ≤)) : ‚áë(f.restrict s) = f ‚àò ((‚Üë) : s ‚Üí Œ±) :=
  rfl

@[simp]
theorem restrict_apply (f : C(Œ±, Œ≤)) (s : Set Œ±) (x : s) : f.restrict s x = f x :=
  rfl

@[simp]
theorem restrict_apply_mk (f : C(Œ±, Œ≤)) (s : Set Œ±) (x : Œ±) (hx : x ‚àà s) :
    f.restrict s ‚ü®x, hx‚ü© = f x :=
  rfl

theorem injective_restrict [T2Space Œ≤] {s : Set Œ±} (hs : Dense s) :
    Injective (restrict s : C(Œ±, Œ≤) ‚Üí C(s, Œ≤)) := fun f g h ‚Ü¶
  DFunLike.ext' <| (map_continuous f).ext_on hs (map_continuous g) <|
    Set.restrict_eq_restrict_iff.1 <| congr_arg DFunLike.coe h

/-- The restriction of a continuous map to the preimage of a set. -/
@[simps]
def restrictPreimage (f : C(Œ±, Œ≤)) (s : Set Œ≤) : C(f ‚Åª¬π' s, s) :=
  ‚ü®s.restrictPreimage f, continuous_iff_continuousAt.mpr fun _ ‚Ü¶
    (map_continuousAt f _).restrictPreimage‚ü©

end Restrict

section mkD

/--
Interpret `f : Œ± ‚Üí Œ≤` as an element of `C(Œ±, Œ≤)`, falling back to the default value
`default : C(Œ±, Œ≤)` if `f` is not continuous.
This is mainly intended to be used for `C(Œ±, Œ≤)`-valued integration. For example, if a family of
functions `f : Œπ ‚Üí Œ± ‚Üí Œ≤` satisfies that `f i` is continuous for almost every `i`, you can write
the `C(Œ±, Œ≤)`-valued integral "`‚à´ i, f i`" as `‚à´ i, ContinuousMap.mkD (f i) 0`.
-/
noncomputable def mkD (f : Œ± ‚Üí Œ≤) (default : C(Œ±, Œ≤)) : C(Œ±, Œ≤) :=
  open scoped Classical in
  if h : Continuous f then ‚ü®_, h‚ü© else default

lemma mkD_of_continuous {f : Œ± ‚Üí Œ≤} {g : C(Œ±, Œ≤)} (hf : Continuous f) :
    mkD f g = ‚ü®f, hf‚ü© := by
  simp only [mkD, hf, ‚ÜìreduceDIte]

lemma mkD_of_not_continuous {f : Œ± ‚Üí Œ≤} {g : C(Œ±, Œ≤)} (hf : ¬¨ Continuous f) :
    mkD f g = g := by
  simp only [mkD, hf, ‚ÜìreduceDIte]

lemma mkD_apply_of_continuous {f : Œ± ‚Üí Œ≤} {g : C(Œ±, Œ≤)} {x : Œ±} (hf : Continuous f) :
    mkD f g x = f x := by
  rw [mkD_of_continuous hf, coe_mk]

lemma mkD_of_continuousOn {s : Set Œ±} {f : Œ± ‚Üí Œ≤} {g : C(s, Œ≤)}
    (hf : ContinuousOn f s) :
    mkD (s.restrict f) g = ‚ü®s.restrict f, hf.restrict‚ü© :=
  mkD_of_continuous hf.restrict

lemma mkD_of_not_continuousOn {s : Set Œ±} {f : Œ± ‚Üí Œ≤} {g : C(s, Œ≤)}
    (hf : ¬¨ ContinuousOn f s) :
    mkD (s.restrict f) g = g := by
  rw [continuousOn_iff_continuous_restrict] at hf
  exact mkD_of_not_continuous hf

lemma mkD_apply_of_continuousOn {s : Set Œ±} {f : Œ± ‚Üí Œ≤} {g : C(s, Œ≤)} {x : s}
    (hf : ContinuousOn f s) :
    mkD (s.restrict f) g x = f x := by
  rw [mkD_of_continuousOn hf, coe_mk, Set.restrict_apply]

lemma mkD_eq_self {f g : C(Œ±, Œ≤)} : mkD f g = f :=
  mkD_of_continuous f.continuous

end mkD

section Gluing

variable {Œπ : Type*} (S : Œπ ‚Üí Set Œ±) (œÜ : ‚àÄ i : Œπ, C(S i, Œ≤))
  (hœÜ : ‚àÄ (i j) (x : Œ±) (hxi : x ‚àà S i) (hxj : x ‚àà S j), œÜ i ‚ü®x, hxi‚ü© = œÜ j ‚ü®x, hxj‚ü©)
  (hS : ‚àÄ x : Œ±, ‚àÉ i, S i ‚àà ùìù x)

/-- A family `œÜ i` of continuous maps `C(S i, Œ≤)`, where the domains `S i` contain a neighbourhood
of each point in `Œ±` and the functions `œÜ i` agree pairwise on intersections, can be glued to
construct a continuous map in `C(Œ±, Œ≤)`. -/
noncomputable def liftCover : C(Œ±, Œ≤) :=
  haveI H : ‚ãÉ i, S i = Set.univ :=
    Set.iUnion_eq_univ_iff.2 fun x ‚Ü¶ (hS x).imp fun _ ‚Ü¶ mem_of_mem_nhds
  mk (Set.liftCover S (fun i ‚Ü¶ œÜ i) hœÜ H) <| continuous_of_cover_nhds hS fun i ‚Ü¶ by
    rw [continuousOn_iff_continuous_restrict]
    simpa +unfoldPartialApp only [Set.restrict, Set.liftCover_coe]
      using map_continuous (œÜ i)

variable {S œÜ hœÜ hS}

@[simp]
theorem liftCover_coe {i : Œπ} (x : S i) : liftCover S œÜ hœÜ hS x = œÜ i x := by
  rw [liftCover, coe_mk, Set.liftCover_coe _]

@[simp]
theorem liftCover_restrict {i : Œπ} : (liftCover S œÜ hœÜ hS).restrict (S i) = œÜ i := by
  ext
  simp only [coe_restrict, Function.comp_apply, liftCover_coe]

variable (A : Set (Set Œ±)) (F : ‚àÄ s ‚àà A, C(s, Œ≤))
  (hF : ‚àÄ (s) (hs : s ‚àà A) (t) (ht : t ‚àà A) (x : Œ±) (hxi : x ‚àà s) (hxj : x ‚àà t),
    F s hs ‚ü®x, hxi‚ü© = F t ht ‚ü®x, hxj‚ü©)
  (hA : ‚àÄ x : Œ±, ‚àÉ i ‚àà A, i ‚àà ùìù x)

/-- A family `F s` of continuous maps `C(s, Œ≤)`, where (1) the domains `s` are taken from a set `A`
of sets in `Œ±` which contain a neighbourhood of each point in `Œ±` and (2) the functions `F s` agree
pairwise on intersections, can be glued to construct a continuous map in `C(Œ±, Œ≤)`. -/
noncomputable def liftCover' : C(Œ±, Œ≤) :=
  let F : ‚àÄ i : A, C(i, Œ≤) := fun i ‚Ü¶ F i i.prop
  liftCover ((‚Üë) : A ‚Üí Set Œ±) F (fun i j ‚Ü¶ hF i i.prop j j.prop)
    fun x ‚Ü¶ let ‚ü®s, hs, hsx‚ü© := hA x; ‚ü®‚ü®s, hs‚ü©, hsx‚ü©

variable {A F hF hA}

-- Porting note: did not need `by delta liftCover'; exact` in mathlib3; goal was
-- closed by `liftCover_coe x'`
-- Might be something to do with the `let`s in the definition of `liftCover'`?
@[simp]
theorem liftCover_coe' {s : Set Œ±} {hs : s ‚àà A} (x : s) : liftCover' A F hF hA x = F s hs x :=
  let x' : ((‚Üë) : A ‚Üí Set Œ±) ‚ü®s, hs‚ü© := x
  by delta liftCover'; exact ContinuousMap.liftCover_coe x'

@[simp]
theorem liftCover_restrict' {s : Set Œ±} {hs : s ‚àà A} :
    (liftCover' A F hF hA).restrict s = F s hs := ext <| liftCover_coe' (hF := hF) (hA := hA)

end Gluing

/-- `Set.inclusion` as a bundled continuous map. -/
def inclusion {s t : Set Œ±} (h : s ‚äÜ t) : C(s, t) where
  toFun := Set.inclusion h
  continuous_toFun := continuous_inclusion h

end ContinuousMap

section Lift

variable {X Y Z : Type*} [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]
    {f : C(X, Y)}

/-- `Setoid.quotientKerEquivOfRightInverse` as a homeomorphism. -/
@[simps!]
def Function.RightInverse.homeomorph {f' : C(Y, X)} (hf : Function.RightInverse f' f) :
    Quotient (Setoid.ker f) ‚âÉ‚Çú Y where
  toEquiv := Setoid.quotientKerEquivOfRightInverse _ _ hf
  continuous_toFun := isQuotientMap_quot_mk.continuous_iff.mpr (map_continuous f)
  continuous_invFun := continuous_quotient_mk'.comp (map_continuous f')

namespace Topology.IsQuotientMap

/--
The homeomorphism from the quotient of a quotient map to its codomain. This is
`Setoid.quotientKerEquivOfSurjective` as a homeomorphism.
-/
@[simps!]
noncomputable def homeomorph (hf : IsQuotientMap f) : Quotient (Setoid.ker f) ‚âÉ‚Çú Y where
  toEquiv := Setoid.quotientKerEquivOfSurjective _ hf.surjective
  continuous_toFun := isQuotientMap_quot_mk.continuous_iff.mpr hf.continuous
  continuous_invFun := by
    rw [hf.continuous_iff]
    convert continuous_quotient_mk'
    ext
    simp only [Equiv.invFun_as_coe, Function.comp_apply,
      (Setoid.quotientKerEquivOfSurjective f hf.surjective).symm_apply_eq]
    rfl

variable (hf : IsQuotientMap f) (g : C(X, Z)) (h : Function.FactorsThrough g f)

/-- Descend a continuous map, which is constant on the fibres, along a quotient map. -/
@[simps]
noncomputable def lift : C(Y, Z) where
  toFun := ((fun i ‚Ü¶ Quotient.liftOn' i g (fun _ _ (hab : f _ = f _) ‚Ü¶ h hab)) :
    Quotient (Setoid.ker f) ‚Üí Z) ‚àò hf.homeomorph.symm
  continuous_toFun := Continuous.comp (continuous_quot_lift _ g.2) (Homeomorph.continuous _)

/--
The obvious triangle induced by `IsQuotientMap.lift` commutes:
```
     g
  X --‚Üí Z
  |   ‚Üó
f |  / hf.lift g h
  v /
  Y
```
-/
@[simp]
theorem lift_comp : (hf.lift g h).comp f = g := by
  ext
  simpa using h (Function.rightInverse_surjInv _ _)

/-- `IsQuotientMap.lift` as an equivalence. -/
@[simps]
noncomputable def liftEquiv : { g : C(X, Z) // Function.FactorsThrough g f} ‚âÉ C(Y, Z) where
  toFun g := hf.lift g g.prop
  invFun g := ‚ü®g.comp f, fun _ _ h ‚Ü¶ by simp only [ContinuousMap.comp_apply]; rw [h]‚ü©
  left_inv := by intro; simp
  right_inv := by
    intro g
    ext a
    simpa using congrArg g (Function.rightInverse_surjInv hf.surjective a)

end Topology.IsQuotientMap
end Lift

namespace Homeomorph

variable {Œ± Œ≤ Œ≥ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [TopologicalSpace Œ≥]
variable (f : Œ± ‚âÉ‚Çú Œ≤) (g : Œ≤ ‚âÉ‚Çú Œ≥)

instance instContinuousMapClass : ContinuousMapClass (Œ± ‚âÉ‚Çú Œ≤) Œ± Œ≤ where
  map_continuous f := f.continuous_toFun

@[simp]
theorem coe_refl : (Homeomorph.refl Œ± : C(Œ±, Œ±)) = ContinuousMap.id Œ± :=
  rfl

@[simp]
theorem coe_trans : (f.trans g : C(Œ±, Œ≥)) = (g : C(Œ≤, Œ≥)).comp f :=
  rfl

/-- Left inverse to a continuous map from a homeomorphism, mirroring `Equiv.symm_comp_self`. -/
@[simp]
theorem symm_comp_toContinuousMap :
    (f.symm : C(Œ≤, Œ±)).comp (f : C(Œ±, Œ≤)) = ContinuousMap.id Œ± := by
  rw [‚Üê coe_trans, self_trans_symm, coe_refl]

/-- Right inverse to a continuous map from a homeomorphism, mirroring `Equiv.self_comp_symm`. -/
@[simp]
theorem toContinuousMap_comp_symm :
    (f : C(Œ±, Œ≤)).comp (f.symm : C(Œ≤, Œ±)) = ContinuousMap.id Œ≤ := by
  rw [‚Üê coe_trans, symm_trans_self, coe_refl]

end Homeomorph
