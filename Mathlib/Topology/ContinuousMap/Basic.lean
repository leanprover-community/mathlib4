/-
Copyright (c) 2020 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri
-/
import Mathlib.Data.Set.UnionLift
import Mathlib.Topology.ContinuousMap.Defs
import Mathlib.Topology.Homeomorph.Defs
import Mathlib.Topology.Separation.Hausdorff

/-!
# Continuous bundled maps

In this file we define the type `ContinuousMap` of continuous bundled maps.

We use the `DFunLike` design, so each type of morphisms has a companion typeclass which is meant to
be satisfied by itself and all stricter types.
-/


open Function Topology

section ContinuousMapClass

variable {F Î± Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²] [FunLike F Î± Î²]
variable [ContinuousMapClass F Î± Î²]

theorem map_continuousAt (f : F) (a : Î±) : ContinuousAt f a :=
  (map_continuous f).continuousAt

theorem map_continuousWithinAt (f : F) (s : Set Î±) (a : Î±) : ContinuousWithinAt f s a :=
  (map_continuous f).continuousWithinAt

end ContinuousMapClass

/-! ### Continuous maps -/


namespace ContinuousMap

variable {Î± Î² Î³ Î´ : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²] [TopologicalSpace Î³]
  [TopologicalSpace Î´]

variable {f g : C(Î±, Î²)}

/-- Deprecated. Use `map_continuousAt` instead. -/
protected theorem continuousAt (f : C(Î±, Î²)) (x : Î±) : ContinuousAt f x :=
  map_continuousAt f x

theorem map_specializes (f : C(Î±, Î²)) {x y : Î±} (h : x â¤³ y) : f x â¤³ f y :=
  h.map f.2

section DiscreteTopology
variable [DiscreteTopology Î±]

/--
The continuous functions from `Î±` to `Î²` are the same as the plain functions when `Î±` is discrete.
-/
@[simps]
def equivFnOfDiscrete : C(Î±, Î²) â‰ƒ (Î± â†’ Î²) :=
  âŸ¨fun f => f,
    fun f => âŸ¨f, continuous_of_discreteTopologyâŸ©,
    fun _ => by ext; rfl,
    fun _ => by ext; rflâŸ©

@[simp] lemma coe_equivFnOfDiscrete : â‡‘equivFnOfDiscrete = (DFunLike.coe : C(Î±, Î²) â†’ Î± â†’ Î²) := rfl

@[simp] lemma equivFnOfDiscrete_symm_apply (f : Î± â†’ Î²) : equivFnOfDiscrete.symm f = f := rfl

end DiscreteTopology

variable (Î±)

/-- The identity as a continuous map. -/
protected def id : C(Î±, Î±) where
  toFun := id

@[simp, norm_cast]
theorem coe_id : â‡‘(ContinuousMap.id Î±) = id :=
  rfl

/-- The constant map as a continuous map. -/
def const (b : Î²) : C(Î±, Î²) where
  toFun := fun _ : Î± => b

@[simp]
theorem coe_const (b : Î²) : â‡‘(const Î± b) = Function.const Î± b :=
  rfl

/-- `Function.const Î± b` as a bundled continuous function of `b`. -/
@[simps -fullyApplied]
def constPi : C(Î², Î± â†’ Î²) where
  toFun b := Function.const Î± b

instance [Inhabited Î²] : Inhabited C(Î±, Î²) :=
  âŸ¨const Î± defaultâŸ©

variable {Î±}

@[simp]
theorem id_apply (a : Î±) : ContinuousMap.id Î± a = a :=
  rfl

@[simp]
theorem const_apply (b : Î²) (a : Î±) : const Î± b a = b :=
  rfl

/-- The composition of continuous maps, as a continuous map. -/
def comp (f : C(Î², Î³)) (g : C(Î±, Î²)) : C(Î±, Î³) where
  toFun := f âˆ˜ g

@[simp]
theorem coe_comp (f : C(Î², Î³)) (g : C(Î±, Î²)) : â‡‘(comp f g) = f âˆ˜ g :=
  rfl

@[simp]
theorem comp_apply (f : C(Î², Î³)) (g : C(Î±, Î²)) (a : Î±) : comp f g a = f (g a) :=
  rfl

@[simp]
theorem comp_assoc (f : C(Î³, Î´)) (g : C(Î², Î³)) (h : C(Î±, Î²)) :
    (f.comp g).comp h = f.comp (g.comp h) :=
  rfl

@[simp]
theorem id_comp (f : C(Î±, Î²)) : (ContinuousMap.id _).comp f = f :=
  ext fun _ => rfl

@[simp]
theorem comp_id (f : C(Î±, Î²)) : f.comp (ContinuousMap.id _) = f :=
  ext fun _ => rfl

@[simp]
theorem const_comp (c : Î³) (f : C(Î±, Î²)) : (const Î² c).comp f = const Î± c :=
  ext fun _ => rfl

@[simp]
theorem comp_const (f : C(Î², Î³)) (b : Î²) : f.comp (const Î± b) = const Î± (f b) :=
  ext fun _ => rfl

@[simp]
theorem cancel_right {fâ‚ fâ‚‚ : C(Î², Î³)} {g : C(Î±, Î²)} (hg : Surjective g) :
    fâ‚.comp g = fâ‚‚.comp g â†” fâ‚ = fâ‚‚ :=
  âŸ¨fun h => ext <| hg.forall.2 <| DFunLike.ext_iff.1 h, congr_arg (ContinuousMap.comp Â· g)âŸ©

@[simp]
theorem cancel_left {f : C(Î², Î³)} {gâ‚ gâ‚‚ : C(Î±, Î²)} (hf : Injective f) :
    f.comp gâ‚ = f.comp gâ‚‚ â†” gâ‚ = gâ‚‚ :=
  âŸ¨fun h => ext fun a => hf <| by rw [â† comp_apply, h, comp_apply], congr_arg _âŸ©

instance [Nonempty Î±] [Nontrivial Î²] : Nontrivial C(Î±, Î²) :=
  âŸ¨let âŸ¨bâ‚, bâ‚‚, hbâŸ© := exists_pair_ne Î²
  âŸ¨const _ bâ‚, const _ bâ‚‚, fun h => hb <| DFunLike.congr_fun h <| Classical.arbitrary Î±âŸ©âŸ©

/-- The bijection `C(Xâ‚, Yâ‚) â‰ƒ C(Xâ‚‚, Yâ‚‚)` induced by homeomorphisms
`e : Xâ‚ â‰ƒâ‚œ Xâ‚‚` and `e' : Yâ‚ â‰ƒâ‚œ Yâ‚‚`. -/
@[simps]
def _root_.Homeomorph.continuousMapCongr {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ : Type*}
    [TopologicalSpace Xâ‚] [TopologicalSpace Xâ‚‚]
    [TopologicalSpace Yâ‚] [TopologicalSpace Yâ‚‚]
    (e : Xâ‚ â‰ƒâ‚œ Xâ‚‚) (e' : Yâ‚ â‰ƒâ‚œ Yâ‚‚) :
    C(Xâ‚, Yâ‚) â‰ƒ C(Xâ‚‚, Yâ‚‚) where
  toFun f := ContinuousMap.comp âŸ¨_, e'.continuousâŸ© (f.comp âŸ¨_, e.symm.continuousâŸ©)
  invFun g := ContinuousMap.comp âŸ¨_, e'.symm.continuousâŸ© (g.comp âŸ¨_, e.continuousâŸ©)
  left_inv _ := by aesop
  right_inv _ := by aesop

section Prod

variable {Î±â‚ Î±â‚‚ Î²â‚ Î²â‚‚ : Type*} [TopologicalSpace Î±â‚] [TopologicalSpace Î±â‚‚] [TopologicalSpace Î²â‚]
  [TopologicalSpace Î²â‚‚]

/-- `Prod.fst : (x, y) â†¦ x` as a bundled continuous map. -/
@[simps -fullyApplied]
def fst : C(Î± Ã— Î², Î±) where
  toFun := Prod.fst

/-- `Prod.snd : (x, y) â†¦ y` as a bundled continuous map. -/
@[simps -fullyApplied]
def snd : C(Î± Ã— Î², Î²) where
  toFun := Prod.snd

/-- Given two continuous maps `f` and `g`, this is the continuous map `x â†¦ (f x, g x)`. -/
def prodMk (f : C(Î±, Î²â‚)) (g : C(Î±, Î²â‚‚)) : C(Î±, Î²â‚ Ã— Î²â‚‚) where
  toFun x := (f x, g x)

/-- Given two continuous maps `f` and `g`, this is the continuous map `(x, y) â†¦ (f x, g y)`. -/
@[simps]
def prodMap (f : C(Î±â‚, Î±â‚‚)) (g : C(Î²â‚, Î²â‚‚)) : C(Î±â‚ Ã— Î²â‚, Î±â‚‚ Ã— Î²â‚‚) where
  toFun := Prod.map f g

@[simp]
theorem prod_eval (f : C(Î±, Î²â‚)) (g : C(Î±, Î²â‚‚)) (a : Î±) : (prodMk f g) a = (f a, g a) :=
  rfl

/-- `Prod.swap` bundled as a `ContinuousMap`. -/
@[simps!]
def prodSwap : C(Î± Ã— Î², Î² Ã— Î±) := .prodMk .snd .fst

end Prod

section Sigma

variable {I A : Type*} {X : I â†’ Type*} [TopologicalSpace A] [âˆ€ i, TopologicalSpace (X i)]

/-- `Sigma.mk i` as a bundled continuous map. -/
@[simps apply]
def sigmaMk (i : I) : C(X i, Î£ i, X i) where
  toFun := Sigma.mk i

/--
To give a continuous map out of a disjoint union, it suffices to give a continuous map out of
each term. This is `Sigma.uncurry` for continuous maps.
-/
@[simps]
def sigma (f : âˆ€ i, C(X i, A)) : C((Î£ i, X i), A) where
  toFun ig := f ig.fst ig.snd

variable (A X) in
/--
Giving a continuous map out of a disjoint union is the same as giving a continuous map out of
each term. This is a version of `Equiv.piCurry` for continuous maps.
-/
@[simps]
def sigmaEquiv : (âˆ€ i, C(X i, A)) â‰ƒ C((Î£ i, X i), A) where
  toFun := sigma
  invFun f i := f.comp (sigmaMk i)

end Sigma

section Pi

variable {I A : Type*} {X Y : I â†’ Type*} [TopologicalSpace A] [âˆ€ i, TopologicalSpace (X i)]
  [âˆ€ i, TopologicalSpace (Y i)]

/-- Abbreviation for product of continuous maps, which is continuous -/
def pi (f : âˆ€ i, C(A, X i)) : C(A, âˆ€ i, X i) where
  toFun (a : A) (i : I) := f i a

@[simp]
theorem pi_eval (f : âˆ€ i, C(A, X i)) (a : A) : (pi f) a = fun i : I => (f i) a :=
  rfl

/-- Evaluation at point as a bundled continuous map. -/
@[simps -fullyApplied]
def eval (i : I) : C(âˆ€ j, X j, X i) where
  toFun := Function.eval i

variable (A X) in
/--
Giving a continuous map out of a disjoint union is the same as giving a continuous map out of
each term
-/
@[simps]
def piEquiv : (âˆ€ i, C(A, X i)) â‰ƒ C(A, âˆ€ i, X i) where
  toFun := pi
  invFun f i := (eval i).comp f

/-- Combine a collection of bundled continuous maps `C(X i, Y i)` into a bundled continuous map
`C(âˆ€ i, X i, âˆ€ i, Y i)`. -/
@[simps!]
def piMap (f : âˆ€ i, C(X i, Y i)) : C((i : I) â†’ X i, (i : I) â†’ Y i) :=
  .pi fun i â†¦ (f i).comp (eval i)

/-- "Precomposition" as a continuous map between dependent types. -/
def precomp {Î¹ : Type*} (Ï† : Î¹ â†’ I) : C((i : I) â†’ X i, (i : Î¹) â†’ X (Ï† i)) :=
  âŸ¨_, Pi.continuous_precomp' Ï†âŸ©

end Pi

section Restrict

variable (s : Set Î±)

/-- The restriction of a continuous function `Î± â†’ Î²` to a subset `s` of `Î±`. -/
def restrict (f : C(Î±, Î²)) : C(s, Î²) where
  toFun := f âˆ˜ ((â†‘) : s â†’ Î±)

@[simp]
theorem coe_restrict (f : C(Î±, Î²)) : â‡‘(f.restrict s) = f âˆ˜ ((â†‘) : s â†’ Î±) :=
  rfl

@[simp]
theorem restrict_apply (f : C(Î±, Î²)) (s : Set Î±) (x : s) : f.restrict s x = f x :=
  rfl

@[simp]
theorem restrict_apply_mk (f : C(Î±, Î²)) (s : Set Î±) (x : Î±) (hx : x âˆˆ s) :
    f.restrict s âŸ¨x, hxâŸ© = f x :=
  rfl

theorem injective_restrict [T2Space Î²] {s : Set Î±} (hs : Dense s) :
    Injective (restrict s : C(Î±, Î²) â†’ C(s, Î²)) := fun f g h â†¦
  DFunLike.ext' <| (map_continuous f).ext_on hs (map_continuous g) <|
    Set.restrict_eq_restrict_iff.1 <| congr_arg DFunLike.coe h

/-- The restriction of a continuous map to the preimage of a set. -/
@[simps]
def restrictPreimage (f : C(Î±, Î²)) (s : Set Î²) : C(f â»Â¹' s, s) :=
  âŸ¨s.restrictPreimage f, continuous_iff_continuousAt.mpr fun _ â†¦
    (map_continuousAt f _).restrictPreimageâŸ©

end Restrict

section mkD

/--
Interpret `f : Î± â†’ Î²` as an element of `C(Î±, Î²)`, falling back to the default value
`default : C(Î±, Î²)` if `f` is not continuous.
This is mainly intended to be used for `C(Î±, Î²)`-valued integration. For example, if a family of
functions `f : Î¹ â†’ Î± â†’ Î²` satisfies that `f i` is continuous for almost every `i`, you can write
the `C(Î±, Î²)`-valued integral "`âˆ« i, f i`" as `âˆ« i, ContinuousMap.mkD (f i) 0`.
-/
noncomputable def mkD (f : Î± â†’ Î²) (default : C(Î±, Î²)) : C(Î±, Î²) :=
  open scoped Classical in
  if h : Continuous f then âŸ¨_, hâŸ© else default

lemma mkD_of_continuous {f : Î± â†’ Î²} {g : C(Î±, Î²)} (hf : Continuous f) :
    mkD f g = âŸ¨f, hfâŸ© := by
  simp only [mkD, hf, â†“reduceDIte]

lemma mkD_of_not_continuous {f : Î± â†’ Î²} {g : C(Î±, Î²)} (hf : Â¬ Continuous f) :
    mkD f g = g := by
  simp only [mkD, hf, â†“reduceDIte]

lemma mkD_apply_of_continuous {f : Î± â†’ Î²} {g : C(Î±, Î²)} {x : Î±} (hf : Continuous f) :
    mkD f g x = f x := by
  rw [mkD_of_continuous hf, coe_mk]

lemma mkD_of_continuousOn {s : Set Î±} {f : Î± â†’ Î²} {g : C(s, Î²)}
    (hf : ContinuousOn f s) :
    mkD (s.restrict f) g = âŸ¨s.restrict f, hf.restrictâŸ© :=
  mkD_of_continuous hf.restrict

lemma mkD_of_not_continuousOn {s : Set Î±} {f : Î± â†’ Î²} {g : C(s, Î²)}
    (hf : Â¬ ContinuousOn f s) :
    mkD (s.restrict f) g = g := by
  rw [continuousOn_iff_continuous_restrict] at hf
  exact mkD_of_not_continuous hf

lemma mkD_apply_of_continuousOn {s : Set Î±} {f : Î± â†’ Î²} {g : C(s, Î²)} {x : s}
    (hf : ContinuousOn f s) :
    mkD (s.restrict f) g x = f x := by
  rw [mkD_of_continuousOn hf, coe_mk, Set.restrict_apply]

lemma mkD_eq_self {f g : C(Î±, Î²)} : mkD f g = f :=
  mkD_of_continuous f.continuous

end mkD

section Gluing

variable {Î¹ : Type*} (S : Î¹ â†’ Set Î±) (Ï† : âˆ€ i : Î¹, C(S i, Î²))
  (hÏ† : âˆ€ (i j) (x : Î±) (hxi : x âˆˆ S i) (hxj : x âˆˆ S j), Ï† i âŸ¨x, hxiâŸ© = Ï† j âŸ¨x, hxjâŸ©)
  (hS : âˆ€ x : Î±, âˆƒ i, S i âˆˆ ð“ x)

/-- A family `Ï† i` of continuous maps `C(S i, Î²)`, where the domains `S i` contain a neighbourhood
of each point in `Î±` and the functions `Ï† i` agree pairwise on intersections, can be glued to
construct a continuous map in `C(Î±, Î²)`. -/
noncomputable def liftCover : C(Î±, Î²) :=
  haveI H : â‹ƒ i, S i = Set.univ :=
    Set.iUnion_eq_univ_iff.2 fun x â†¦ (hS x).imp fun _ â†¦ mem_of_mem_nhds
  mk (Set.liftCover S (fun i â†¦ Ï† i) hÏ† H) <| continuous_of_cover_nhds hS fun i â†¦ by
    rw [continuousOn_iff_continuous_restrict]
    simpa +unfoldPartialApp only [Set.restrict, Set.liftCover_coe]
      using map_continuous (Ï† i)

variable {S Ï† hÏ† hS}

@[simp]
theorem liftCover_coe {i : Î¹} (x : S i) : liftCover S Ï† hÏ† hS x = Ï† i x := by
  rw [liftCover, coe_mk, Set.liftCover_coe _]

@[simp]
theorem liftCover_restrict {i : Î¹} : (liftCover S Ï† hÏ† hS).restrict (S i) = Ï† i := by
  ext
  simp only [coe_restrict, Function.comp_apply, liftCover_coe]

variable (A : Set (Set Î±)) (F : âˆ€ s âˆˆ A, C(s, Î²))
  (hF : âˆ€ (s) (hs : s âˆˆ A) (t) (ht : t âˆˆ A) (x : Î±) (hxi : x âˆˆ s) (hxj : x âˆˆ t),
    F s hs âŸ¨x, hxiâŸ© = F t ht âŸ¨x, hxjâŸ©)
  (hA : âˆ€ x : Î±, âˆƒ i âˆˆ A, i âˆˆ ð“ x)

/-- A family `F s` of continuous maps `C(s, Î²)`, where (1) the domains `s` are taken from a set `A`
of sets in `Î±` which contain a neighbourhood of each point in `Î±` and (2) the functions `F s` agree
pairwise on intersections, can be glued to construct a continuous map in `C(Î±, Î²)`. -/
noncomputable def liftCover' : C(Î±, Î²) :=
  let F : âˆ€ i : A, C(i, Î²) := fun i => F i i.prop
  liftCover ((â†‘) : A â†’ Set Î±) F (fun i j => hF i i.prop j j.prop)
    fun x => let âŸ¨s, hs, hsxâŸ© := hA x; âŸ¨âŸ¨s, hsâŸ©, hsxâŸ©

variable {A F hF hA}

-- Porting note: did not need `by delta liftCover'; exact` in mathlib3; goal was
-- closed by `liftCover_coe x'`
-- Might be something to do with the `let`s in the definition of `liftCover'`?
@[simp]
theorem liftCover_coe' {s : Set Î±} {hs : s âˆˆ A} (x : s) : liftCover' A F hF hA x = F s hs x :=
  let x' : ((â†‘) : A â†’ Set Î±) âŸ¨s, hsâŸ© := x
  by delta liftCover'; exact ContinuousMap.liftCover_coe x'

@[simp]
theorem liftCover_restrict' {s : Set Î±} {hs : s âˆˆ A} :
    (liftCover' A F hF hA).restrict s = F s hs := ext <| liftCover_coe' (hF := hF) (hA := hA)

end Gluing

/-- `Set.inclusion` as a bundled continuous map. -/
def inclusion {s t : Set Î±} (h : s âŠ† t) : C(s, t) where
  toFun := Set.inclusion h
  continuous_toFun := continuous_inclusion h

end ContinuousMap

section Lift

variable {X Y Z : Type*} [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]
    {f : C(X, Y)}

/-- `Setoid.quotientKerEquivOfRightInverse` as a homeomorphism. -/
@[simps!]
def Function.RightInverse.homeomorph {f' : C(Y, X)} (hf : Function.RightInverse f' f) :
    Quotient (Setoid.ker f) â‰ƒâ‚œ Y where
  toEquiv := Setoid.quotientKerEquivOfRightInverse _ _ hf
  continuous_toFun := isQuotientMap_quot_mk.continuous_iff.mpr (map_continuous f)
  continuous_invFun := continuous_quotient_mk'.comp (map_continuous f')

namespace Topology.IsQuotientMap

/--
The homeomorphism from the quotient of a quotient map to its codomain. This is
`Setoid.quotientKerEquivOfSurjective` as a homeomorphism.
-/
@[simps!]
noncomputable def homeomorph (hf : IsQuotientMap f) : Quotient (Setoid.ker f) â‰ƒâ‚œ Y where
  toEquiv := Setoid.quotientKerEquivOfSurjective _ hf.surjective
  continuous_toFun := isQuotientMap_quot_mk.continuous_iff.mpr hf.continuous
  continuous_invFun := by
    rw [hf.continuous_iff]
    convert continuous_quotient_mk'
    ext
    simp only [Equiv.invFun_as_coe, Function.comp_apply,
      (Setoid.quotientKerEquivOfSurjective f hf.surjective).symm_apply_eq]
    rfl

variable (hf : IsQuotientMap f) (g : C(X, Z)) (h : Function.FactorsThrough g f)

/-- Descend a continuous map, which is constant on the fibres, along a quotient map. -/
@[simps]
noncomputable def lift : C(Y, Z) where
  toFun := ((fun i â†¦ Quotient.liftOn' i g (fun _ _ (hab : f _ = f _) â†¦ h hab)) :
    Quotient (Setoid.ker f) â†’ Z) âˆ˜ hf.homeomorph.symm
  continuous_toFun := Continuous.comp (continuous_quot_lift _ g.2) (Homeomorph.continuous _)

/--
The obvious triangle induced by `IsQuotientMap.lift` commutes:
```
     g
  X --â†’ Z
  |   â†—
f |  / hf.lift g h
  v /
  Y
```
-/
@[simp]
theorem lift_comp : (hf.lift g h).comp f = g := by
  ext
  simpa using h (Function.rightInverse_surjInv _ _)

/-- `IsQuotientMap.lift` as an equivalence. -/
@[simps]
noncomputable def liftEquiv : { g : C(X, Z) // Function.FactorsThrough g f} â‰ƒ C(Y, Z) where
  toFun g := hf.lift g g.prop
  invFun g := âŸ¨g.comp f, fun _ _ h â†¦ by simp only [ContinuousMap.comp_apply]; rw [h]âŸ©
  left_inv := by intro; simp
  right_inv := by
    intro g
    ext a
    simpa using congrArg g (Function.rightInverse_surjInv hf.surjective a)

end Topology.IsQuotientMap
end Lift

namespace Homeomorph

variable {Î± Î² Î³ : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²] [TopologicalSpace Î³]
variable (f : Î± â‰ƒâ‚œ Î²) (g : Î² â‰ƒâ‚œ Î³)

instance instContinuousMapClass : ContinuousMapClass (Î± â‰ƒâ‚œ Î²) Î± Î² where
  map_continuous f := f.continuous_toFun

@[simp]
theorem coe_refl : (Homeomorph.refl Î± : C(Î±, Î±)) = ContinuousMap.id Î± :=
  rfl

@[simp]
theorem coe_trans : (f.trans g : C(Î±, Î³)) = (g : C(Î², Î³)).comp f :=
  rfl

/-- Left inverse to a continuous map from a homeomorphism, mirroring `Equiv.symm_comp_self`. -/
@[simp]
theorem symm_comp_toContinuousMap :
    (f.symm : C(Î², Î±)).comp (f : C(Î±, Î²)) = ContinuousMap.id Î± := by
  rw [â† coe_trans, self_trans_symm, coe_refl]

/-- Right inverse to a continuous map from a homeomorphism, mirroring `Equiv.self_comp_symm`. -/
@[simp]
theorem toContinuousMap_comp_symm :
    (f : C(Î±, Î²)).comp (f.symm : C(Î², Î±)) = ContinuousMap.id Î² := by
  rw [â† coe_trans, symm_trans_self, coe_refl]

end Homeomorph
