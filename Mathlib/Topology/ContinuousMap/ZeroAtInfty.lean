/-
Copyright (c) 2022 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Topology.ContinuousMap.Bounded.Star
import Mathlib.Topology.ContinuousMap.CocompactMap

/-!
# Continuous functions vanishing at infinity

The type of continuous functions vanishing at infinity. When the domain is compact
`C(Œ±, Œ≤) ‚âÉ C‚ÇÄ(Œ±, Œ≤)` via the identity map. When the codomain is a metric space, every continuous
map which vanishes at infinity is a bounded continuous function. When the domain is a locally
compact space, this type has nice properties.

## TODO

* Create more instances of algebraic structures (e.g., `NonUnitalSemiring`) once the necessary
  type classes (e.g., `IsTopologicalRing`) are sufficiently generalized.
* Relate the unitization of `C‚ÇÄ(Œ±, Œ≤)` to the Alexandroff compactification.
-/


universe u v w

variable {F : Type*} {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w} [TopologicalSpace Œ±]

open BoundedContinuousFunction Topology Bornology

open Filter Metric

/-- `C‚ÇÄ(Œ±, Œ≤)` is the type of continuous functions `Œ± ‚Üí Œ≤` which vanish at infinity from a
topological space to a metric space with a zero element.

When possible, instead of parametrizing results over `(f : C‚ÇÄ(Œ±, Œ≤))`,
you should parametrize over `(F : Type*) [ZeroAtInftyContinuousMapClass F Œ± Œ≤] (f : F)`.

When you extend this structure, make sure to extend `ZeroAtInftyContinuousMapClass`. -/
structure ZeroAtInftyContinuousMap (Œ± : Type u) (Œ≤ : Type v) [TopologicalSpace Œ±] [Zero Œ≤]
    [TopologicalSpace Œ≤] : Type max u v extends ContinuousMap Œ± Œ≤ where
  /-- The function tends to zero along the `cocompact` filter. -/
  zero_at_infty' : Tendsto toFun (cocompact Œ±) (ùìù 0)

@[inherit_doc]
scoped[ZeroAtInfty] notation (priority := 2000) "C‚ÇÄ(" Œ± ", " Œ≤ ")" => ZeroAtInftyContinuousMap Œ± Œ≤

@[inherit_doc]
scoped[ZeroAtInfty] notation Œ± " ‚ÜíC‚ÇÄ " Œ≤ => ZeroAtInftyContinuousMap Œ± Œ≤

open ZeroAtInfty

section

/-- `ZeroAtInftyContinuousMapClass F Œ± Œ≤` states that `F` is a type of continuous maps which
vanish at infinity.

You should also extend this typeclass when you extend `ZeroAtInftyContinuousMap`. -/
class ZeroAtInftyContinuousMapClass (F : Type*) (Œ± Œ≤ : outParam Type*) [TopologicalSpace Œ±]
    [Zero Œ≤] [TopologicalSpace Œ≤] [FunLike F Œ± Œ≤] : Prop extends ContinuousMapClass F Œ± Œ≤ where
  /-- Each member of the class tends to zero along the `cocompact` filter. -/
  zero_at_infty (f : F) : Tendsto f (cocompact Œ±) (ùìù 0)

end

export ZeroAtInftyContinuousMapClass (zero_at_infty)

namespace ZeroAtInftyContinuousMap

section Basics

variable [TopologicalSpace Œ≤] [Zero Œ≤] [FunLike F Œ± Œ≤] [ZeroAtInftyContinuousMapClass F Œ± Œ≤]

instance instFunLike : FunLike C‚ÇÄ(Œ±, Œ≤) Œ± Œ≤ where
  coe f := f.toFun
  coe_injective' f g h := by
    obtain ‚ü®‚ü®_, _‚ü©, _‚ü© := f
    obtain ‚ü®‚ü®_, _‚ü©, _‚ü© := g
    congr

instance instZeroAtInftyContinuousMapClass : ZeroAtInftyContinuousMapClass C‚ÇÄ(Œ±, Œ≤) Œ± Œ≤ where
  map_continuous f := f.continuous_toFun
  zero_at_infty f := f.zero_at_infty'

instance instCoeTC : CoeTC F C‚ÇÄ(Œ±, Œ≤) :=
  ‚ü®fun f =>
    { toFun := f
      continuous_toFun := map_continuous f
      zero_at_infty' := zero_at_infty f }‚ü©

@[simp]
theorem coe_toContinuousMap (f : C‚ÇÄ(Œ±, Œ≤)) : (f.toContinuousMap : Œ± ‚Üí Œ≤) = f :=
  rfl

@[ext]
theorem ext {f g : C‚ÇÄ(Œ±, Œ≤)} (h : ‚àÄ x, f x = g x) : f = g :=
  DFunLike.ext _ _ h

@[simp]
lemma coe_mk {f : Œ± ‚Üí Œ≤} (hf : Continuous f) (hf' : Tendsto f (cocompact Œ±) (ùìù 0)) :
    { toFun := f,
      continuous_toFun := hf,
      zero_at_infty' := hf' : ZeroAtInftyContinuousMap Œ± Œ≤} = f :=
  rfl

/-- Copy of a `ZeroAtInftyContinuousMap` with a new `toFun` equal to the old one. Useful
to fix definitional equalities. -/
protected def copy (f : C‚ÇÄ(Œ±, Œ≤)) (f' : Œ± ‚Üí Œ≤) (h : f' = f) : C‚ÇÄ(Œ±, Œ≤) where
  toFun := f'
  continuous_toFun := by
    rw [h]
    exact f.continuous_toFun
  zero_at_infty' := by
    simp_rw [h]
    exact f.zero_at_infty'

@[simp]
theorem coe_copy (f : C‚ÇÄ(Œ±, Œ≤)) (f' : Œ± ‚Üí Œ≤) (h : f' = f) : ‚áë(f.copy f' h) = f' :=
  rfl

theorem copy_eq (f : C‚ÇÄ(Œ±, Œ≤)) (f' : Œ± ‚Üí Œ≤) (h : f' = f) : f.copy f' h = f :=
  DFunLike.ext' h

theorem eq_of_empty [IsEmpty Œ±] (f g : C‚ÇÄ(Œ±, Œ≤)) : f = g :=
  ext <| IsEmpty.elim ‚Äπ_‚Ä∫

/-- A continuous function on a compact space is automatically a continuous function vanishing at
infinity. -/
@[simps]
def ContinuousMap.liftZeroAtInfty [CompactSpace Œ±] : C(Œ±, Œ≤) ‚âÉ C‚ÇÄ(Œ±, Œ≤) where
  toFun f :=
    { toFun := f
      continuous_toFun := f.continuous
      zero_at_infty' := by simp }
  invFun f := f

/-- A continuous function on a compact space is automatically a continuous function vanishing at
infinity. This is not an instance to avoid type class loops. -/
lemma zeroAtInftyContinuousMapClass.ofCompact {G : Type*} [FunLike G Œ± Œ≤]
    [ContinuousMapClass G Œ± Œ≤] [CompactSpace Œ±] : ZeroAtInftyContinuousMapClass G Œ± Œ≤ where
  map_continuous := map_continuous
  zero_at_infty := by simp

end Basics

/-! ### Algebraic structure

Whenever `Œ≤` has suitable algebraic structure and a compatible topological structure, then
`C‚ÇÄ(Œ±, Œ≤)` inherits a corresponding algebraic structure. The primary exception to this is that
`C‚ÇÄ(Œ±, Œ≤)` will not have a multiplicative identity.
-/


section AlgebraicStructure

variable [TopologicalSpace Œ≤] (x : Œ±)

instance instZero [Zero Œ≤] : Zero C‚ÇÄ(Œ±, Œ≤) :=
  ‚ü®‚ü®0, tendsto_const_nhds‚ü©‚ü©

instance instInhabited [Zero Œ≤] : Inhabited C‚ÇÄ(Œ±, Œ≤) :=
  ‚ü®0‚ü©

@[simp]
theorem coe_zero [Zero Œ≤] : ‚áë(0 : C‚ÇÄ(Œ±, Œ≤)) = 0 :=
  rfl

theorem zero_apply [Zero Œ≤] : (0 : C‚ÇÄ(Œ±, Œ≤)) x = 0 :=
  rfl

instance instMul [MulZeroClass Œ≤] [ContinuousMul Œ≤] : Mul C‚ÇÄ(Œ±, Œ≤) :=
  ‚ü®fun f g =>
    ‚ü®f * g, by simpa only [mul_zero] using (zero_at_infty f).mul (zero_at_infty g)‚ü©‚ü©

@[simp]
theorem coe_mul [MulZeroClass Œ≤] [ContinuousMul Œ≤] (f g : C‚ÇÄ(Œ±, Œ≤)) : ‚áë(f * g) = f * g :=
  rfl

theorem mul_apply [MulZeroClass Œ≤] [ContinuousMul Œ≤] (f g : C‚ÇÄ(Œ±, Œ≤)) : (f * g) x = f x * g x :=
  rfl

instance instMulZeroClass [MulZeroClass Œ≤] [ContinuousMul Œ≤] : MulZeroClass C‚ÇÄ(Œ±, Œ≤) :=
  DFunLike.coe_injective.mulZeroClass _ coe_zero coe_mul

instance instSemigroupWithZero [SemigroupWithZero Œ≤] [ContinuousMul Œ≤] :
    SemigroupWithZero C‚ÇÄ(Œ±, Œ≤) :=
  DFunLike.coe_injective.semigroupWithZero _ coe_zero coe_mul

instance instAdd [AddZeroClass Œ≤] [ContinuousAdd Œ≤] : Add C‚ÇÄ(Œ±, Œ≤) :=
  ‚ü®fun f g => ‚ü®f + g, by simpa only [add_zero] using (zero_at_infty f).add (zero_at_infty g)‚ü©‚ü©

@[simp]
theorem coe_add [AddZeroClass Œ≤] [ContinuousAdd Œ≤] (f g : C‚ÇÄ(Œ±, Œ≤)) : ‚áë(f + g) = f + g :=
  rfl

theorem add_apply [AddZeroClass Œ≤] [ContinuousAdd Œ≤] (f g : C‚ÇÄ(Œ±, Œ≤)) : (f + g) x = f x + g x :=
  rfl

instance instAddZeroClass [AddZeroClass Œ≤] [ContinuousAdd Œ≤] : AddZeroClass C‚ÇÄ(Œ±, Œ≤) :=
  DFunLike.coe_injective.addZeroClass _ coe_zero coe_add

instance instSMul [Zero Œ≤] {R : Type*} [Zero R] [SMulWithZero R Œ≤] [ContinuousConstSMul R Œ≤] :
    SMul R C‚ÇÄ(Œ±, Œ≤) :=
  -- Porting note: Original version didn't have `Continuous.const_smul f.continuous r`
  ‚ü®fun r f => ‚ü®‚ü®r ‚Ä¢ ‚áëf, Continuous.const_smul f.continuous r‚ü©,
    by simpa [smul_zero] using (zero_at_infty f).const_smul r‚ü©‚ü©

@[simp, norm_cast]
theorem coe_smul [Zero Œ≤] {R : Type*} [Zero R] [SMulWithZero R Œ≤] [ContinuousConstSMul R Œ≤] (r : R)
    (f : C‚ÇÄ(Œ±, Œ≤)) : ‚áë(r ‚Ä¢ f) = r ‚Ä¢ ‚áëf :=
  rfl

theorem smul_apply [Zero Œ≤] {R : Type*} [Zero R] [SMulWithZero R Œ≤] [ContinuousConstSMul R Œ≤]
    (r : R) (f : C‚ÇÄ(Œ±, Œ≤)) (x : Œ±) : (r ‚Ä¢ f) x = r ‚Ä¢ f x :=
  rfl

section AddMonoid

variable [AddMonoid Œ≤] [ContinuousAdd Œ≤] (f g : C‚ÇÄ(Œ±, Œ≤))

instance instAddMonoid : AddMonoid C‚ÇÄ(Œ±, Œ≤) :=
  DFunLike.coe_injective.addMonoid _ coe_zero coe_add fun _ _ => rfl

end AddMonoid

instance instAddCommMonoid [AddCommMonoid Œ≤] [ContinuousAdd Œ≤] : AddCommMonoid C‚ÇÄ(Œ±, Œ≤) :=
  DFunLike.coe_injective.addCommMonoid _ coe_zero coe_add fun _ _ => rfl

section AddGroup

variable [AddGroup Œ≤] [IsTopologicalAddGroup Œ≤] (f g : C‚ÇÄ(Œ±, Œ≤))

instance instNeg : Neg C‚ÇÄ(Œ±, Œ≤) :=
  ‚ü®fun f => ‚ü®-f, by simpa only [neg_zero] using (zero_at_infty f).neg‚ü©‚ü©

@[simp]
theorem coe_neg : ‚áë(-f) = -f :=
  rfl

theorem neg_apply : (-f) x = -f x :=
  rfl

instance instSub : Sub C‚ÇÄ(Œ±, Œ≤) :=
  ‚ü®fun f g => ‚ü®f - g, by simpa only [sub_zero] using (zero_at_infty f).sub (zero_at_infty g)‚ü©‚ü©

@[simp]
theorem coe_sub : ‚áë(f - g) = f - g :=
  rfl

theorem sub_apply : (f - g) x = f x - g x :=
  rfl

instance instAddGroup : AddGroup C‚ÇÄ(Œ±, Œ≤) :=
  DFunLike.coe_injective.addGroup _ coe_zero coe_add coe_neg coe_sub (fun _ _ => rfl) fun _ _ => rfl

end AddGroup

instance instAddCommGroup [AddCommGroup Œ≤] [IsTopologicalAddGroup Œ≤] : AddCommGroup C‚ÇÄ(Œ±, Œ≤) :=
  DFunLike.coe_injective.addCommGroup _ coe_zero coe_add coe_neg coe_sub (fun _ _ => rfl) fun _ _ =>
    rfl

instance instIsCentralScalar [Zero Œ≤] {R : Type*} [Zero R] [SMulWithZero R Œ≤] [SMulWithZero R·µê·µí·µñ Œ≤]
    [ContinuousConstSMul R Œ≤] [IsCentralScalar R Œ≤] : IsCentralScalar R C‚ÇÄ(Œ±, Œ≤) :=
  ‚ü®fun _ _ => ext fun _ => op_smul_eq_smul _ _‚ü©

instance instSMulWithZero [Zero Œ≤] {R : Type*} [Zero R] [SMulWithZero R Œ≤]
    [ContinuousConstSMul R Œ≤] : SMulWithZero R C‚ÇÄ(Œ±, Œ≤) :=
  Function.Injective.smulWithZero ‚ü®_, coe_zero‚ü© DFunLike.coe_injective coe_smul

instance instMulActionWithZero [Zero Œ≤] {R : Type*} [MonoidWithZero R] [MulActionWithZero R Œ≤]
    [ContinuousConstSMul R Œ≤] : MulActionWithZero R C‚ÇÄ(Œ±, Œ≤) :=
  Function.Injective.mulActionWithZero ‚ü®_, coe_zero‚ü© DFunLike.coe_injective coe_smul

instance instModule [AddCommMonoid Œ≤] [ContinuousAdd Œ≤] {R : Type*} [Semiring R] [Module R Œ≤]
    [ContinuousConstSMul R Œ≤] : Module R C‚ÇÄ(Œ±, Œ≤) :=
  Function.Injective.module R ‚ü®‚ü®_, coe_zero‚ü©, coe_add‚ü© DFunLike.coe_injective coe_smul

instance instNonUnitalNonAssocSemiring [NonUnitalNonAssocSemiring Œ≤] [IsTopologicalSemiring Œ≤] :
    NonUnitalNonAssocSemiring C‚ÇÄ(Œ±, Œ≤) :=
  DFunLike.coe_injective.nonUnitalNonAssocSemiring _ coe_zero coe_add coe_mul fun _ _ => rfl

instance instNonUnitalSemiring [NonUnitalSemiring Œ≤] [IsTopologicalSemiring Œ≤] :
    NonUnitalSemiring C‚ÇÄ(Œ±, Œ≤) :=
  DFunLike.coe_injective.nonUnitalSemiring _ coe_zero coe_add coe_mul fun _ _ => rfl

instance instNonUnitalCommSemiring [NonUnitalCommSemiring Œ≤] [IsTopologicalSemiring Œ≤] :
    NonUnitalCommSemiring C‚ÇÄ(Œ±, Œ≤) :=
  DFunLike.coe_injective.nonUnitalCommSemiring _ coe_zero coe_add coe_mul fun _ _ => rfl

instance instNonUnitalNonAssocRing [NonUnitalNonAssocRing Œ≤] [IsTopologicalRing Œ≤] :
    NonUnitalNonAssocRing C‚ÇÄ(Œ±, Œ≤) :=
  DFunLike.coe_injective.nonUnitalNonAssocRing _ coe_zero coe_add coe_mul coe_neg coe_sub
    (fun _ _ => rfl) fun _ _ => rfl

instance instNonUnitalRing [NonUnitalRing Œ≤] [IsTopologicalRing Œ≤] : NonUnitalRing C‚ÇÄ(Œ±, Œ≤) :=
  DFunLike.coe_injective.nonUnitalRing _ coe_zero coe_add coe_mul coe_neg coe_sub (fun _ _ => rfl)
    fun _ _ => rfl

instance instNonUnitalCommRing [NonUnitalCommRing Œ≤] [IsTopologicalRing Œ≤] :
    NonUnitalCommRing C‚ÇÄ(Œ±, Œ≤) :=
  DFunLike.coe_injective.nonUnitalCommRing _ coe_zero coe_add coe_mul coe_neg coe_sub
    (fun _ _ => rfl) fun _ _ => rfl

instance instIsScalarTower {R : Type*} [Semiring R] [NonUnitalNonAssocSemiring Œ≤]
    [IsTopologicalSemiring Œ≤] [Module R Œ≤] [ContinuousConstSMul R Œ≤] [IsScalarTower R Œ≤ Œ≤] :
    IsScalarTower R C‚ÇÄ(Œ±, Œ≤) C‚ÇÄ(Œ±, Œ≤) where
  smul_assoc r f g := by
    ext
    simp only [smul_eq_mul, coe_mul, coe_smul, Pi.mul_apply, Pi.smul_apply]
    rw [‚Üê smul_eq_mul, ‚Üê smul_eq_mul, smul_assoc]

instance instSMulCommClass {R : Type*} [Semiring R] [NonUnitalNonAssocSemiring Œ≤]
    [IsTopologicalSemiring Œ≤] [Module R Œ≤] [ContinuousConstSMul R Œ≤] [SMulCommClass R Œ≤ Œ≤] :
    SMulCommClass R C‚ÇÄ(Œ±, Œ≤) C‚ÇÄ(Œ±, Œ≤) where
  smul_comm r f g := by
    ext
    simp only [smul_eq_mul, coe_smul, coe_mul, Pi.smul_apply, Pi.mul_apply]
    rw [‚Üê smul_eq_mul, ‚Üê smul_eq_mul, smul_comm]

end AlgebraicStructure

section Uniform

variable [UniformSpace Œ≤] [UniformSpace Œ≥] [Zero Œ≥]
variable [FunLike F Œ≤ Œ≥] [ZeroAtInftyContinuousMapClass F Œ≤ Œ≥]

theorem uniformContinuous (f : F) : UniformContinuous (f : Œ≤ ‚Üí Œ≥) :=
  (map_continuous f).uniformContinuous_of_tendsto_cocompact (zero_at_infty f)

end Uniform

/-! ### Metric structure

When `Œ≤` is a metric space, then every element of `C‚ÇÄ(Œ±, Œ≤)` is bounded, and so there is a natural
inclusion map `ZeroAtInftyContinuousMap.toBCF : C‚ÇÄ(Œ±, Œ≤) ‚Üí (Œ± ‚Üí·µá Œ≤)`. Via this map `C‚ÇÄ(Œ±, Œ≤)`
inherits a metric as the pullback of the metric on `Œ± ‚Üí·µá Œ≤`. Moreover, this map has closed range
in `Œ± ‚Üí·µá Œ≤` and consequently `C‚ÇÄ(Œ±, Œ≤)` is a complete space whenever `Œ≤` is complete.
-/


section Metric

open Metric Set

variable [PseudoMetricSpace Œ≤] [Zero Œ≤] [FunLike F Œ± Œ≤] [ZeroAtInftyContinuousMapClass F Œ± Œ≤]

protected theorem bounded (f : F) : ‚àÉ C, ‚àÄ x y : Œ±, dist ((f : Œ± ‚Üí Œ≤) x) (f y) ‚â§ C := by
  obtain ‚ü®K : Set Œ±, hK‚ÇÅ, hK‚ÇÇ‚ü© := mem_cocompact.mp
    (tendsto_def.mp (zero_at_infty (f : F)) _ (closedBall_mem_nhds (0 : Œ≤) zero_lt_one))
  obtain ‚ü®C, hC‚ü© := (hK‚ÇÅ.image (map_continuous f)).isBounded.subset_closedBall (0 : Œ≤)
  refine ‚ü®max C 1 + max C 1, fun x y => ?_‚ü©
  have : ‚àÄ x, f x ‚àà closedBall (0 : Œ≤) (max C 1) := by
    intro x
    by_cases hx : x ‚àà K
    ¬∑ exact (mem_closedBall.mp <| hC ‚ü®x, hx, rfl‚ü©).trans (le_max_left _ _)
    ¬∑ exact (mem_closedBall.mp <| mem_preimage.mp (hK‚ÇÇ hx)).trans (le_max_right _ _)
  exact (dist_triangle (f x) 0 (f y)).trans
    (add_le_add (mem_closedBall.mp <| this x) (mem_closedBall'.mp <| this y))

theorem isBounded_range (f : C‚ÇÄ(Œ±, Œ≤)) : IsBounded (range f) :=
  isBounded_range_iff.2 (ZeroAtInftyContinuousMap.bounded f)

theorem isBounded_image (f : C‚ÇÄ(Œ±, Œ≤)) (s : Set Œ±) : IsBounded (f '' s) :=
  f.isBounded_range.subset <| image_subset_range _ _

instance (priority := 100) instBoundedContinuousMapClass : BoundedContinuousMapClass F Œ± Œ≤ :=
  { ‚ÄπZeroAtInftyContinuousMapClass F Œ± Œ≤‚Ä∫ with
    map_bounded := fun f => ZeroAtInftyContinuousMap.bounded f }

/-- Construct a bounded continuous function from a continuous function vanishing at infinity. -/
@[simps!]
def toBCF (f : C‚ÇÄ(Œ±, Œ≤)) : Œ± ‚Üí·µá Œ≤ :=
  ‚ü®f, map_bounded f‚ü©

section

variable (Œ±) (Œ≤)

theorem toBCF_injective : Function.Injective (toBCF : C‚ÇÄ(Œ±, Œ≤) ‚Üí Œ± ‚Üí·µá Œ≤) := fun f g h => by
  ext x
  simpa only using DFunLike.congr_fun h x

end

variable {f g : C‚ÇÄ(Œ±, Œ≤)}

/-- The type of continuous functions vanishing at infinity, with the uniform distance induced by the
inclusion `ZeroAtInftyContinuousMap.toBCF`, is a pseudo-metric space. -/
noncomputable instance instPseudoMetricSpace : PseudoMetricSpace C‚ÇÄ(Œ±, Œ≤) :=
  PseudoMetricSpace.induced toBCF inferInstance

/-- The type of continuous functions vanishing at infinity, with the uniform distance induced by the
inclusion `ZeroAtInftyContinuousMap.toBCF`, is a metric space. -/
noncomputable instance instMetricSpace {Œ≤ : Type*} [MetricSpace Œ≤] [Zero Œ≤] :
    MetricSpace C‚ÇÄ(Œ±, Œ≤) :=
  MetricSpace.induced _ (toBCF_injective Œ± Œ≤) inferInstance

@[simp]
theorem dist_toBCF_eq_dist {f g : C‚ÇÄ(Œ±, Œ≤)} : dist f.toBCF g.toBCF = dist f g :=
  rfl

open BoundedContinuousFunction

/-- Convergence in the metric on `C‚ÇÄ(Œ±, Œ≤)` is uniform convergence. -/
theorem tendsto_iff_tendstoUniformly {Œπ : Type*} {F : Œπ ‚Üí C‚ÇÄ(Œ±, Œ≤)} {f : C‚ÇÄ(Œ±, Œ≤)} {l : Filter Œπ} :
    Tendsto F l (ùìù f) ‚Üî TendstoUniformly (fun i => F i) f l := by
  simpa only [Metric.tendsto_nhds] using
    @BoundedContinuousFunction.tendsto_iff_tendstoUniformly _ _ _ _ _ (fun i => (F i).toBCF)
      f.toBCF l

theorem isometry_toBCF : Isometry (toBCF : C‚ÇÄ(Œ±, Œ≤) ‚Üí Œ± ‚Üí·µá Œ≤) := by tauto

theorem isClosed_range_toBCF : IsClosed (range (toBCF : C‚ÇÄ(Œ±, Œ≤) ‚Üí Œ± ‚Üí·µá Œ≤)) := by
  refine isClosed_iff_clusterPt.mpr fun f hf => ?_
  rw [clusterPt_principal_iff] at hf
  have : Tendsto f (cocompact Œ±) (ùìù 0) := by
    refine Metric.tendsto_nhds.mpr fun Œµ hŒµ => ?_
    obtain ‚ü®_, hg, g, rfl‚ü© := hf (ball f (Œµ / 2)) (ball_mem_nhds f <| half_pos hŒµ)
    refine (Metric.tendsto_nhds.mp (zero_at_infty g) (Œµ / 2) (half_pos hŒµ)).mp
      (Eventually.of_forall fun x hx => ?_)
    calc
      dist (f x) 0 ‚â§ dist (g.toBCF x) (f x) + dist (g x) 0 := dist_triangle_left _ _ _
      _ < dist g.toBCF f + Œµ / 2 := add_lt_add_of_le_of_lt (dist_coe_le_dist x) hx
      _ < Œµ := by simpa [add_halves Œµ] using add_lt_add_right (mem_ball.1 hg) (Œµ / 2)
  exact ‚ü®‚ü®f.toContinuousMap, this‚ü©, rfl‚ü©


/-- Continuous functions vanishing at infinity taking values in a complete space form a
complete space. -/
instance instCompleteSpace [CompleteSpace Œ≤] : CompleteSpace C‚ÇÄ(Œ±, Œ≤) :=
  (completeSpace_iff_isComplete_range isometry_toBCF.isUniformInducing).mpr
    isClosed_range_toBCF.isComplete

end Metric

section Norm

/-! ### Normed space

The norm structure on `C‚ÇÄ(Œ±, Œ≤)` is the one induced by the inclusion `toBCF : C‚ÇÄ(Œ±, Œ≤) ‚Üí (Œ± ‚Üí·µá b)`,
viewed as an additive monoid homomorphism. Then `C‚ÇÄ(Œ±, Œ≤)` is naturally a normed space over a normed
field `ùïú` whenever `Œ≤` is as well.
-/


section NormedSpace

noncomputable instance instSeminormedAddCommGroup [SeminormedAddCommGroup Œ≤] :
    SeminormedAddCommGroup C‚ÇÄ(Œ±, Œ≤) :=
  SeminormedAddCommGroup.induced _ _ (‚ü®‚ü®toBCF, rfl‚ü©, fun _ _ => rfl‚ü© : C‚ÇÄ(Œ±, Œ≤) ‚Üí+ Œ± ‚Üí·µá Œ≤)

noncomputable instance instNormedAddCommGroup [NormedAddCommGroup Œ≤] :
    NormedAddCommGroup C‚ÇÄ(Œ±, Œ≤) :=
  NormedAddCommGroup.induced _ _ (‚ü®‚ü®toBCF, rfl‚ü©, fun _ _ => rfl‚ü© : C‚ÇÄ(Œ±, Œ≤) ‚Üí+ Œ± ‚Üí·µá Œ≤)
    (toBCF_injective Œ± Œ≤)

variable [SeminormedAddCommGroup Œ≤] {ùïú : Type*} [NormedField ùïú] [NormedSpace ùïú Œ≤]

@[simp]
theorem norm_toBCF_eq_norm {f : C‚ÇÄ(Œ±, Œ≤)} : ‚Äñf.toBCF‚Äñ = ‚Äñf‚Äñ :=
  rfl

noncomputable instance : NormedSpace ùïú C‚ÇÄ(Œ±, Œ≤) where
  norm_smul_le k f := norm_smul_le k f.toBCF

end NormedSpace

section NormedRing

noncomputable instance instNonUnitalSeminormedRing [NonUnitalSeminormedRing Œ≤] :
    NonUnitalSeminormedRing C‚ÇÄ(Œ±, Œ≤) :=
  { instNonUnitalRing, instSeminormedAddCommGroup with
    norm_mul_le f g := norm_mul_le f.toBCF g.toBCF }

noncomputable instance instNonUnitalNormedRing [NonUnitalNormedRing Œ≤] :
    NonUnitalNormedRing C‚ÇÄ(Œ±, Œ≤) :=
  { instNonUnitalSeminormedRing, instNormedAddCommGroup with }

noncomputable instance instNonUnitalSeminormedCommRing [NonUnitalSeminormedCommRing Œ≤] :
    NonUnitalSeminormedCommRing C‚ÇÄ(Œ±, Œ≤) :=
  { instNonUnitalSeminormedRing, instNonUnitalCommRing with }

noncomputable instance instNonUnitalNormedCommRing [NonUnitalNormedCommRing Œ≤] :
    NonUnitalNormedCommRing C‚ÇÄ(Œ±, Œ≤) :=
  { instNonUnitalNormedRing, instNonUnitalCommRing with }

end NormedRing

end Norm

section Star

/-! ### Star structure

It is possible to equip `C‚ÇÄ(Œ±, Œ≤)` with a pointwise `star` operation whenever there is a continuous
`star : Œ≤ ‚Üí Œ≤` for which `star (0 : Œ≤) = 0`. We don't have quite this weak a typeclass, but
`StarAddMonoid` is close enough.

The `StarAddMonoid` and `NormedStarGroup` classes on `C‚ÇÄ(Œ±, Œ≤)` are inherited from their
counterparts on `Œ± ‚Üí·µá Œ≤`. Ultimately, when `Œ≤` is a C‚ãÜ-ring, then so is `C‚ÇÄ(Œ±, Œ≤)`.
-/


variable [TopologicalSpace Œ≤] [AddMonoid Œ≤] [StarAddMonoid Œ≤] [ContinuousStar Œ≤]

instance instStar : Star C‚ÇÄ(Œ±, Œ≤) where
  star f :=
    { toFun := fun x => star (f x)
      continuous_toFun := (map_continuous f).star
      zero_at_infty' := by
        simpa only [star_zero] using (continuous_star.tendsto (0 : Œ≤)).comp (zero_at_infty f) }

@[simp]
theorem coe_star (f : C‚ÇÄ(Œ±, Œ≤)) : ‚áë(star f) = star (‚áëf) :=
  rfl

theorem star_apply (f : C‚ÇÄ(Œ±, Œ≤)) (x : Œ±) : (star f) x = star (f x) :=
  rfl

instance instStarAddMonoid [ContinuousAdd Œ≤] : StarAddMonoid C‚ÇÄ(Œ±, Œ≤) where
  star_involutive f := ext fun x => star_star (f x)
  star_add f g := ext fun x => star_add (f x) (g x)

end Star

section NormedStar

variable [NormedAddCommGroup Œ≤] [StarAddMonoid Œ≤] [NormedStarGroup Œ≤]

instance instNormedStarGroup : NormedStarGroup C‚ÇÄ(Œ±, Œ≤) where
  norm_star_le f := (norm_star f.toBCF :).le

end NormedStar

section StarModule

variable {ùïú : Type*} [Zero ùïú] [Star ùïú] [AddMonoid Œ≤] [StarAddMonoid Œ≤] [TopologicalSpace Œ≤]
  [ContinuousStar Œ≤] [SMulWithZero ùïú Œ≤] [ContinuousConstSMul ùïú Œ≤] [StarModule ùïú Œ≤]

instance instStarModule : StarModule ùïú C‚ÇÄ(Œ±, Œ≤) where
  star_smul k f := ext fun x => star_smul k (f x)

end StarModule

section StarRing

variable [NonUnitalSemiring Œ≤] [StarRing Œ≤] [TopologicalSpace Œ≤] [ContinuousStar Œ≤]
  [IsTopologicalSemiring Œ≤]

instance instStarRing : StarRing C‚ÇÄ(Œ±, Œ≤) :=
  { ZeroAtInftyContinuousMap.instStarAddMonoid with
    star_mul := fun f g => ext fun x => star_mul (f x) (g x) }

end StarRing

section CStarRing

instance instCStarRing [NonUnitalNormedRing Œ≤] [StarRing Œ≤] [CStarRing Œ≤] : CStarRing C‚ÇÄ(Œ±, Œ≤) where
  norm_mul_self_le f := CStarRing.norm_mul_self_le (x := f.toBCF)

end CStarRing

/-! ### C‚ÇÄ as a functor

For each `Œ≤` with sufficient structure, there is a contravariant functor `C‚ÇÄ(-, Œ≤)` from the
category of topological spaces with morphisms given by `CocompactMap`s.
-/


variable {Œ¥ : Type*} [TopologicalSpace Œ≤] [TopologicalSpace Œ≥] [TopologicalSpace Œ¥]

local notation Œ± " ‚Üíco " Œ≤ => CocompactMap Œ± Œ≤

section

variable [Zero Œ¥]

/-- Composition of a continuous function vanishing at infinity with a cocompact map yields another
continuous function vanishing at infinity. -/
def comp (f : C‚ÇÄ(Œ≥, Œ¥)) (g : Œ≤ ‚Üíco Œ≥) : C‚ÇÄ(Œ≤, Œ¥) where
  toContinuousMap := (f : C(Œ≥, Œ¥)).comp g
  zero_at_infty' := (zero_at_infty f).comp (cocompact_tendsto g)

@[simp]
theorem coe_comp_to_continuous_fun (f : C‚ÇÄ(Œ≥, Œ¥)) (g : Œ≤ ‚Üíco Œ≥) : ((f.comp g) : Œ≤ ‚Üí Œ¥) = f ‚àò g :=
  rfl

@[simp]
theorem comp_id (f : C‚ÇÄ(Œ≥, Œ¥)) : f.comp (CocompactMap.id Œ≥) = f :=
  ext fun _ => rfl

@[simp]
theorem comp_assoc (f : C‚ÇÄ(Œ≥, Œ¥)) (g : Œ≤ ‚Üíco Œ≥) (h : Œ± ‚Üíco Œ≤) :
    (f.comp g).comp h = f.comp (g.comp h) :=
  rfl

@[simp]
theorem zero_comp (g : Œ≤ ‚Üíco Œ≥) : (0 : C‚ÇÄ(Œ≥, Œ¥)).comp g = 0 :=
  rfl

end

/-- Composition as an additive monoid homomorphism. -/
def compAddMonoidHom [AddMonoid Œ¥] [ContinuousAdd Œ¥] (g : Œ≤ ‚Üíco Œ≥) : C‚ÇÄ(Œ≥, Œ¥) ‚Üí+ C‚ÇÄ(Œ≤, Œ¥) where
  toFun f := f.comp g
  map_zero' := zero_comp g
  map_add' _ _ := rfl

/-- Composition as a semigroup homomorphism. -/
def compMulHom [MulZeroClass Œ¥] [ContinuousMul Œ¥] (g : Œ≤ ‚Üíco Œ≥) : C‚ÇÄ(Œ≥, Œ¥) ‚Üí‚Çô* C‚ÇÄ(Œ≤, Œ¥) where
  toFun f := f.comp g
  map_mul' _ _ := rfl

/-- Composition as a linear map. -/
def compLinearMap [AddCommMonoid Œ¥] [ContinuousAdd Œ¥] {R : Type*} [Semiring R] [Module R Œ¥]
    [ContinuousConstSMul R Œ¥] (g : Œ≤ ‚Üíco Œ≥) : C‚ÇÄ(Œ≥, Œ¥) ‚Üí‚Çó[R] C‚ÇÄ(Œ≤, Œ¥) where
  toFun f := f.comp g
  map_add' _ _ := rfl
  map_smul' _ _ := rfl

/-- Composition as a non-unital algebra homomorphism. -/
def compNonUnitalAlgHom {R : Type*} [Semiring R] [NonUnitalNonAssocSemiring Œ¥]
    [IsTopologicalSemiring Œ¥] [Module R Œ¥] [ContinuousConstSMul R Œ¥] (g : Œ≤ ‚Üíco Œ≥) :
    C‚ÇÄ(Œ≥, Œ¥) ‚Üí‚Çô‚Çê[R] C‚ÇÄ(Œ≤, Œ¥) where
  toFun f := f.comp g
  map_smul' _ _ := rfl
  map_zero' := rfl
  map_add' _ _ := rfl
  map_mul' _ _ := rfl

end ZeroAtInftyContinuousMap
