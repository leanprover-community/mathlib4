/-
Copyright (c) 2024 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Topology.CompactOpen

/-!
# Second countable topology on `C(X, Y)`

In this file we prove that `C(X, Y)` with compact-open topology has second countable topology, if

- both `X` and `Y` have second countable topology;
- `X` is a locally compact space;
-/

open scoped Topology
open Set Function Filter TopologicalSpace

namespace ContinuousMap

variable {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]

theorem compactOpen_eq_generateFrom {S : Set (Set X)} {T : Set (Set Y)}
    (hSâ‚ : âˆ€ K âˆˆ S, IsCompact K) (hT : IsTopologicalBasis T)
    (hSâ‚‚ : âˆ€ f : C(X, Y), âˆ€ x, âˆ€ V âˆˆ T, f x âˆˆ V â†’ âˆƒ K âˆˆ S, K âˆˆ ğ“ x âˆ§ MapsTo f K V) :
    compactOpen = .generateFrom (.image2 (fun K t â†¦
      {f : C(X, Y) | MapsTo f K (â‹ƒâ‚€ t)}) S {t : Set (Set Y) | t.Finite âˆ§ t âŠ† T}) := by
  apply le_antisymm
  Â· apply_rules [generateFrom_anti, image2_subset_iff.mpr]
    intro K hK t ht
    exact mem_image2_of_mem (hSâ‚ K hK) (isOpen_sUnion fun _ h â†¦ hT.isOpen <| ht.2 h)
  Â· refine le_of_nhds_le_nhds fun f â†¦ ?_
    simp only [nhds_compactOpen, le_iInf_iff, le_principal_iff]
    intro K (hK : IsCompact K) U (hU : IsOpen U) hfKU
    simp only [TopologicalSpace.nhds_generateFrom]
    obtain âŸ¨t, htT, htf, hTU, hKTâŸ© : âˆƒ t âŠ† T, t.Finite âˆ§ (âˆ€ V âˆˆ t, V âŠ† U) âˆ§ f '' K âŠ† â‹ƒâ‚€ t := by
      rw [hT.open_eq_sUnion' hU, mapsTo_iff_image_subset, sUnion_eq_biUnion] at hfKU
      obtain âŸ¨t, ht, hfin, htKâŸ© :=
        (hK.image (map_continuous f)).elim_finite_subcover_image (fun V hV â†¦ hT.isOpen hV.1) hfKU
      refine âŸ¨t, fun _ h â†¦ (ht h).1, hfin, fun _ h â†¦ (ht h).2, ?_âŸ©
      rwa [sUnion_eq_biUnion]
    rw [image_subset_iff] at hKT
    obtain âŸ¨s, hsS, hsf, hKs, hstâŸ© : âˆƒ s âŠ† S, s.Finite âˆ§ K âŠ† â‹ƒâ‚€ s âˆ§ MapsTo f (â‹ƒâ‚€ s) (â‹ƒâ‚€ t) := by
      have : âˆ€ x âˆˆ K, âˆƒ L âˆˆ S, L âˆˆ ğ“ x âˆ§ MapsTo f L (â‹ƒâ‚€ t) := by
        intro x hx
        rcases hKT hx with âŸ¨V, hVt, hxVâŸ©
        rcases hSâ‚‚ f x V (htT hVt) hxV with âŸ¨L, hLS, hLx, hLVâŸ©
        exact âŸ¨L, hLS, hLx, hLV.mono_right <| subset_sUnion_of_mem hVtâŸ©
      choose! L hLS hLmem hLt using this
      rcases hK.elim_nhds_subcover L hLmem with âŸ¨s, hsK, hsâŸ©
      refine âŸ¨L '' s, image_subset_iff.2 fun x hx â†¦ hLS x <| hsK x hx, s.finite_toSet.image _,
        by rwa [sUnion_image], ?_âŸ©
      rw [mapsTo_sUnion, forall_mem_image]
      exact fun x hx â†¦ hLt x <| hsK x hx
    have hsub : (â‹‚ L âˆˆ s, {g : C(X, Y) | MapsTo g L (â‹ƒâ‚€ t)}) âŠ† {g | MapsTo g K U} := by
      simp only [â† setOf_forall, â† mapsTo_iUnion, â† sUnion_eq_biUnion]
      exact fun g hg â†¦ hg.mono hKs (sUnion_subset hTU)
    refine mem_of_superset ((biInter_mem hsf).2 fun L hL â†¦ ?_) hsub
    refine mem_iInf_of_mem _ <| mem_iInf_of_mem ?_ <| mem_principal_self _
    exact âŸ¨hst.mono_left (subset_sUnion_of_mem hL), mem_image2_of_mem (hsS hL) âŸ¨htf, htTâŸ©âŸ©

/-- A version of `instSecondCountableTopology` with a technical assumption
instead of `[SecondCountableTopology X] [LocallyCompactSpace X]`.
It is here as a reminder of what could be an intermediate goal,
if someone tries to weaken the assumptions in the instance
(e.g., from `[LocallyCompactSpace X]` to `[LocallyCompactPair X Y]` - not sure if it's true). -/
theorem secondCountableTopology [SecondCountableTopology Y]
    (hX : âˆƒ S : Set (Set X), S.Countable âˆ§ (âˆ€ K âˆˆ S, IsCompact K) âˆ§
      âˆ€ f : C(X, Y), âˆ€ V, IsOpen V â†’ âˆ€ x âˆˆ f â»Â¹' V, âˆƒ K âˆˆ S, K âˆˆ ğ“ x âˆ§ MapsTo f K V) :
    SecondCountableTopology C(X, Y) where
  is_open_generated_countable := by
    rcases hX with âŸ¨S, hScount, hScomp, hSâŸ©
    refine âŸ¨_, ?_, compactOpen_eq_generateFrom (S := S) hScomp (isBasis_countableBasis _) ?_âŸ©
    Â· exact .image2 hScount (countable_setOf_finite_subset (countable_countableBasis Y)) _
    Â· intro f x V hV hx
      apply hS
      exacts [isOpen_of_mem_countableBasis hV, hx]

instance instSecondCountableTopology [SecondCountableTopology X] [LocallyCompactSpace X]
    [SecondCountableTopology Y] : SecondCountableTopology C(X, Y) := by
  apply secondCountableTopology
  have (U : countableBasis X) : LocallyCompactSpace U.1 :=
    (isOpen_of_mem_countableBasis U.2).locallyCompactSpace
  set K := fun U : countableBasis X â†¦ CompactExhaustion.choice U.1
  use â‹ƒ U : countableBasis X, Set.range fun n â†¦ K U n
  refine âŸ¨countable_iUnion fun _ â†¦ countable_range _, ?_, ?_âŸ©
  Â· simp only [mem_iUnion, mem_range]
    rintro K âŸ¨U, n, rflâŸ©
    exact ((K U).isCompact _).image continuous_subtype_val
  Â· intro f V hVo x hxV
    obtain âŸ¨U, hU, hxU, hUVâŸ© : âˆƒ U âˆˆ countableBasis X, x âˆˆ U âˆ§ U âŠ† f â»Â¹' V := by
      rw [â† (isBasis_countableBasis _).mem_nhds_iff]
      exact (hVo.preimage (map_continuous f)).mem_nhds hxV
    lift x to U using hxU
    lift U to countableBasis X using hU
    rcases (K U).exists_mem_nhds x with âŸ¨n, hnâŸ©
    refine âŸ¨K U n, mem_iUnion.2 âŸ¨U, mem_range_self _âŸ©, ?_, ?_âŸ©
    Â· rw [â† map_nhds_subtype_coe_eq_nhds x.2]
      exacts [image_mem_map hn, (isOpen_of_mem_countableBasis U.2).mem_nhds x.2]
    Â· rw [mapsTo_image_iff]
      exact fun y _ â†¦ hUV y.2

instance instSeparableSpace [SecondCountableTopology X] [LocallyCompactSpace X]
    [SecondCountableTopology Y] : SeparableSpace C(X, Y) :=
  inferInstance

end ContinuousMap
