/-
Copyright (c) 2024 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
module

public import Mathlib.Topology.CompactOpen

/-!
# Second countable topology on `C(X, Y)`

In this file we prove that `C(X, Y)` with compact-open topology has second countable topology, if

- both `X` and `Y` have second countable topology;
- `X` is a locally compact space;
-/

@[expose] public section

open scoped Topology
open Set Function Filter TopologicalSpace

namespace ContinuousMap

variable {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]

theorem compactOpen_eq_generateFrom {S : Set (Set X)} {T : Set (Set Y)}
    (hS‚ÇÅ : ‚àÄ K ‚àà S, IsCompact K) (hT : IsTopologicalBasis T)
    (hS‚ÇÇ : ‚àÄ f : C(X, Y), ‚àÄ x, ‚àÄ V ‚àà T, f x ‚àà V ‚Üí ‚àÉ K ‚àà S, K ‚àà ùìù x ‚àß MapsTo f K V) :
    compactOpen = .generateFrom (.image2 (fun K t ‚Ü¶
      {f : C(X, Y) | MapsTo f K (‚ãÉ‚ÇÄ t)}) S {t : Set (Set Y) | t.Finite ‚àß t ‚äÜ T}) := by
  apply le_antisymm
  ¬∑ apply_rules [generateFrom_anti, image2_subset_iff.mpr]
    intro K hK t ht
    exact mem_image2_of_mem (hS‚ÇÅ K hK) (isOpen_sUnion fun _ h ‚Ü¶ hT.isOpen <| ht.2 h)
  ¬∑ refine le_of_nhds_le_nhds fun f ‚Ü¶ ?_
    simp only [nhds_compactOpen, le_iInf_iff, le_principal_iff]
    intro K (hK : IsCompact K) U (hU : IsOpen U) hfKU
    simp only [TopologicalSpace.nhds_generateFrom]
    obtain ‚ü®t, htT, htf, hTU, hKT‚ü© : ‚àÉ t ‚äÜ T, t.Finite ‚àß (‚àÄ V ‚àà t, V ‚äÜ U) ‚àß f '' K ‚äÜ ‚ãÉ‚ÇÄ t := by
      rw [hT.open_eq_sUnion' hU, mapsTo_iff_image_subset, sUnion_eq_biUnion] at hfKU
      obtain ‚ü®t, ht, hfin, htK‚ü© :=
        (hK.image (map_continuous f)).elim_finite_subcover_image (fun V hV ‚Ü¶ hT.isOpen hV.1) hfKU
      refine ‚ü®t, fun _ h ‚Ü¶ (ht h).1, hfin, fun _ h ‚Ü¶ (ht h).2, ?_‚ü©
      rwa [sUnion_eq_biUnion]
    rw [image_subset_iff] at hKT
    obtain ‚ü®s, hsS, hsf, hKs, hst‚ü© : ‚àÉ s ‚äÜ S, s.Finite ‚àß K ‚äÜ ‚ãÉ‚ÇÄ s ‚àß MapsTo f (‚ãÉ‚ÇÄ s) (‚ãÉ‚ÇÄ t) := by
      have : ‚àÄ x ‚àà K, ‚àÉ L ‚àà S, L ‚àà ùìù x ‚àß MapsTo f L (‚ãÉ‚ÇÄ t) := by
        intro x hx
        rcases hKT hx with ‚ü®V, hVt, hxV‚ü©
        rcases hS‚ÇÇ f x V (htT hVt) hxV with ‚ü®L, hLS, hLx, hLV‚ü©
        exact ‚ü®L, hLS, hLx, hLV.mono_right <| subset_sUnion_of_mem hVt‚ü©
      choose! L hLS hLmem hLt using this
      rcases hK.elim_nhds_subcover L hLmem with ‚ü®s, hsK, hs‚ü©
      refine ‚ü®L '' s, image_subset_iff.2 fun x hx ‚Ü¶ hLS x <| hsK x hx, s.finite_toSet.image _,
        by rwa [sUnion_image], ?_‚ü©
      rw [mapsTo_sUnion, forall_mem_image]
      exact fun x hx ‚Ü¶ hLt x <| hsK x hx
    have hsub : (‚ãÇ L ‚àà s, {g : C(X, Y) | MapsTo g L (‚ãÉ‚ÇÄ t)}) ‚äÜ {g | MapsTo g K U} := by
      simp only [‚Üê setOf_forall, ‚Üê mapsTo_iUnion, ‚Üê sUnion_eq_biUnion]
      exact fun g hg ‚Ü¶ hg.mono hKs (sUnion_subset hTU)
    refine mem_of_superset ((biInter_mem hsf).2 fun L hL ‚Ü¶ ?_) hsub
    refine mem_iInf_of_mem _ <| mem_iInf_of_mem ?_ <| mem_principal_self _
    exact ‚ü®hst.mono_left (subset_sUnion_of_mem hL), mem_image2_of_mem (hsS hL) ‚ü®htf, htT‚ü©‚ü©

/-- A version of `instSecondCountableTopology` with a technical assumption
instead of `[SecondCountableTopology X] [LocallyCompactSpace X]`.
It is here as a reminder of what could be an intermediate goal,
if someone tries to weaken the assumptions in the instance
(e.g., from `[LocallyCompactSpace X]` to `[LocallyCompactPair X Y]` - not sure if it's true). -/
theorem secondCountableTopology [SecondCountableTopology Y]
    (hX : ‚àÉ S : Set (Set X), S.Countable ‚àß (‚àÄ K ‚àà S, IsCompact K) ‚àß
      ‚àÄ f : C(X, Y), ‚àÄ V, IsOpen V ‚Üí ‚àÄ x ‚àà f ‚Åª¬π' V, ‚àÉ K ‚àà S, K ‚àà ùìù x ‚àß MapsTo f K V) :
    SecondCountableTopology C(X, Y) where
  is_open_generated_countable := by
    rcases hX with ‚ü®S, hScount, hScomp, hS‚ü©
    refine ‚ü®_, ?_, compactOpen_eq_generateFrom (S := S) hScomp (isBasis_countableBasis _) ?_‚ü©
    ¬∑ exact .image2 hScount (countable_setOf_finite_subset (countable_countableBasis Y)) _
    ¬∑ intro f x V hV hx
      apply hS
      exacts [isOpen_of_mem_countableBasis hV, hx]

instance instSecondCountableTopology [SecondCountableTopology X] [LocallyCompactSpace X]
    [SecondCountableTopology Y] : SecondCountableTopology C(X, Y) := by
  apply secondCountableTopology
  have (U : countableBasis X) : LocallyCompactSpace U.1 :=
    (isOpen_of_mem_countableBasis U.2).locallyCompactSpace
  set K := fun U : countableBasis X ‚Ü¶ CompactExhaustion.choice U.1
  use ‚ãÉ U : countableBasis X, Set.range fun n ‚Ü¶ K U n
  refine ‚ü®countable_iUnion fun _ ‚Ü¶ countable_range _, ?_, ?_‚ü©
  ¬∑ simp only [mem_iUnion, mem_range]
    rintro K ‚ü®U, n, rfl‚ü©
    exact ((K U).isCompact _).image continuous_subtype_val
  ¬∑ intro f V hVo x hxV
    obtain ‚ü®U, hU, hxU, hUV‚ü© : ‚àÉ U ‚àà countableBasis X, x ‚àà U ‚àß U ‚äÜ f ‚Åª¬π' V := by
      rw [‚Üê (isBasis_countableBasis _).mem_nhds_iff]
      exact (hVo.preimage (map_continuous f)).mem_nhds hxV
    lift x to U using hxU
    lift U to countableBasis X using hU
    rcases (K U).exists_mem_nhds x with ‚ü®n, hn‚ü©
    refine ‚ü®K U n, mem_iUnion.2 ‚ü®U, mem_range_self _‚ü©, ?_, ?_‚ü©
    ¬∑ rw [‚Üê map_nhds_subtype_coe_eq_nhds x.2]
      exacts [image_mem_map hn, (isOpen_of_mem_countableBasis U.2).mem_nhds x.2]
    ¬∑ rw [mapsTo_image_iff]
      exact fun y _ ‚Ü¶ hUV y.2

instance instSeparableSpace [SecondCountableTopology X] [LocallyCompactSpace X]
    [SecondCountableTopology Y] : SeparableSpace C(X, Y) :=
  inferInstance

end ContinuousMap
