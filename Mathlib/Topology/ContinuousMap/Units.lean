/-
Copyright (c) 2022 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Analysis.Normed.Ring.Units
import Mathlib.Algebra.Algebra.Spectrum.Basic
import Mathlib.Topology.ContinuousMap.Algebra

/-!
# Units of continuous functions

This file concerns itself with `C(X, M)Ë£` and `C(X, MË£)` when `X` is a topological space
and `M` has some monoid structure compatible with its topology.
-/


variable {X M R ğ•œ : Type*} [TopologicalSpace X]

namespace ContinuousMap

section Monoid

variable [Monoid M] [TopologicalSpace M] [ContinuousMul M]

/-- Equivalence between continuous maps into the units of a monoid with continuous multiplication
and the units of the monoid of continuous maps. -/
-- `simps` generates some lemmas here with LHS not in simp normal form,
-- so we write them out manually below.
@[to_additive (attr := simps apply_val_apply symm_apply_apply_val)
"Equivalence between continuous maps into the additive units of an additive monoid with continuous
addition and the additive units of the additive monoid of continuous maps."]
def unitsLift : C(X, MË£) â‰ƒ C(X, M)Ë£ where
  toFun f :=
    { val := âŸ¨fun x => f x, Units.continuous_val.comp f.continuousâŸ©
      inv := âŸ¨fun x => â†‘(f x)â»Â¹, Units.continuous_val.comp (continuous_inv.comp f.continuous)âŸ©
      val_inv := ext fun _ => Units.mul_inv _
      inv_val := ext fun _ => Units.inv_mul _ }
  invFun f :=
    { toFun := fun x =>
        âŸ¨(f : C(X, M)) x, (â†‘fâ»Â¹ : C(X, M)) x,
          ContinuousMap.congr_fun f.mul_inv x, ContinuousMap.congr_fun f.inv_mul xâŸ©
      continuous_toFun := continuous_induced_rng.2 <|
        (f : C(X, M)).continuous.prodMk <|
        MulOpposite.continuous_op.comp (â†‘fâ»Â¹ : C(X, M)).continuous }

@[to_additive (attr := simp)]
lemma unitsLift_apply_inv_apply (f : C(X, MË£)) (x : X) :
    (â†‘(ContinuousMap.unitsLift f)â»Â¹ : C(X, M)) x = (f x)â»Â¹ :=
  rfl

@[to_additive (attr := simp)]
lemma unitsLift_symm_apply_apply_inv' (f : C(X, M)Ë£) (x : X) :
    (ContinuousMap.unitsLift.symm f x)â»Â¹ = (â†‘fâ»Â¹ : C(X, M)) x := by
  rfl

end Monoid

section NormedRing

variable [NormedRing R] [CompleteSpace R]

theorem continuous_isUnit_unit {f : C(X, R)} (h : âˆ€ x, IsUnit (f x)) :
    Continuous fun x => (h x).unit := by
  refine
    continuous_induced_rng.2
      (Continuous.prodMk f.continuous
        (MulOpposite.continuous_op.comp (continuous_iff_continuousAt.mpr fun x => ?_)))
  have := NormedRing.inverse_continuousAt (h x).unit
  simp only
  simp only [â† Ring.inverse_unit, IsUnit.unit_spec] at this âŠ¢
  exact this.comp (f.continuousAt x)

/-- Construct a continuous map into the group of units of a normed ring from a function into the
normed ring and a proof that every element of the range is a unit. -/
@[simps]
noncomputable def unitsOfForallIsUnit {f : C(X, R)} (h : âˆ€ x, IsUnit (f x)) : C(X, RË£) where
  toFun x := (h x).unit
  continuous_toFun := continuous_isUnit_unit h

instance canLift :
    CanLift C(X, R) C(X, RË£) (fun f => âŸ¨fun x => f x, Units.continuous_val.comp f.continuousâŸ©)
      fun f => âˆ€ x, IsUnit (f x) where
  prf f h := âŸ¨unitsOfForallIsUnit h, by ext; rflâŸ©

theorem isUnit_iff_forall_isUnit (f : C(X, R)) : IsUnit f â†” âˆ€ x, IsUnit (f x) :=
  Iff.intro (fun h => fun x => âŸ¨unitsLift.symm h.unit x, rflâŸ©) fun h =>
    âŸ¨ContinuousMap.unitsLift (unitsOfForallIsUnit h), by ext; rflâŸ©

end NormedRing

section NormedField

variable [NormedField ğ•œ] [NormedDivisionRing R] [Algebra ğ•œ R] [CompleteSpace R]

theorem isUnit_iff_forall_ne_zero (f : C(X, R)) : IsUnit f â†” âˆ€ x, f x â‰  0 := by
  simp_rw [f.isUnit_iff_forall_isUnit, isUnit_iff_ne_zero]

theorem spectrum_eq_preimage_range (f : C(X, R)) :
    spectrum ğ•œ f = algebraMap _ _ â»Â¹' Set.range f := by
  ext x
  simp only [spectrum.mem_iff, isUnit_iff_forall_ne_zero, not_forall, sub_apply,
    algebraMap_apply, mul_one, Classical.not_not, Set.mem_range,
    sub_eq_zero, @eq_comm _ (x â€¢ 1 : R) _, Set.mem_preimage, Algebra.algebraMap_eq_smul_one,
    smul_apply, one_apply]

theorem spectrum_eq_range [CompleteSpace ğ•œ] (f : C(X, ğ•œ)) : spectrum ğ•œ f = Set.range f := by
  rw [spectrum_eq_preimage_range, Algebra.id.map_eq_id]
  exact Set.preimage_id

end NormedField

end ContinuousMap
