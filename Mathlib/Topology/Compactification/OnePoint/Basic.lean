/-
Copyright (c) 2021 Yourong Zang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yourong Zang, Yury Kudryashov
-/
import Mathlib.Data.Fintype.Option
import Mathlib.Topology.Homeomorph.Lemmas
import Mathlib.Topology.Sets.Opens

/-!
# The OnePoint Compactification

We construct the OnePoint compactification (the one-point compactification) of an arbitrary
topological space `X` and prove some properties inherited from `X`.

## Main definitions

* `OnePoint`: the OnePoint compactification, we use coercion for the canonical embedding
  `X â†’ OnePoint X`; when `X` is already compact, the compactification adds an isolated point
  to the space.
* `OnePoint.infty`: the extra point

## Main results

* The topological structure of `OnePoint X`
* The connectedness of `OnePoint X` for a noncompact, preconnected `X`
* `OnePoint X` is `Tâ‚€` for a Tâ‚€ space `X`
* `OnePoint X` is `Tâ‚` for a Tâ‚ space `X`
* `OnePoint X` is normal if `X` is a locally compact Hausdorff space

## Tags

one-point compactification, Alexandroff compactification, compactness
-/


open Set Filter Topology

/-!
### Definition and basic properties

In this section we define `OnePoint X` to be the disjoint union of `X` and `âˆ`, implemented as
`Option X`. Then we restate some lemmas about `Option X` for `OnePoint X`.
-/


variable {X Y : Type*}

/-- The OnePoint extension of an arbitrary topological space `X` -/
def OnePoint (X : Type*) :=
  Option X

/-- The repr uses the notation from the `OnePoint` locale. -/
instance [Repr X] : Repr (OnePoint X) :=
  âŸ¨fun o _ =>
    match o with
    | none => "âˆ"
    | some a => "â†‘" ++ repr aâŸ©

namespace OnePoint

/-- The point at infinity -/
@[match_pattern] def infty : OnePoint X := none

@[inherit_doc]
scoped notation "âˆ" => OnePoint.infty

/-- Coercion from `X` to `OnePoint X`. -/
@[coe, match_pattern] def some : X â†’ OnePoint X := Option.some

@[simp]
lemma some_eq_iff (xâ‚ xâ‚‚ : X) : (some xâ‚ = some xâ‚‚) â†” (xâ‚ = xâ‚‚) := by
  rw [iff_eq_eq]
  exact Option.some.injEq xâ‚ xâ‚‚

instance : CoeTC X (OnePoint X) := âŸ¨someâŸ©

instance : Inhabited (OnePoint X) := âŸ¨âˆâŸ©

protected lemma Â«forallÂ» {p : OnePoint X â†’ Prop} :
    (âˆ€ (x : OnePoint X), p x) â†” p âˆ âˆ§ âˆ€ (x : X), p x :=
  Option.forall

protected lemma Â«existsÂ» {p : OnePoint X â†’ Prop} :
    (âˆƒ x, p x) â†” p âˆ âˆ¨ âˆƒ (x : X), p x :=
  Option.exists

instance [Fintype X] : Fintype (OnePoint X) :=
  inferInstanceAs (Fintype (Option X))

instance infinite [Infinite X] : Infinite (OnePoint X) :=
  inferInstanceAs (Infinite (Option X))

theorem coe_injective : Function.Injective ((â†‘) : X â†’ OnePoint X) :=
  Option.some_injective X

@[norm_cast]
theorem coe_eq_coe {x y : X} : (x : OnePoint X) = y â†” x = y :=
  coe_injective.eq_iff

@[simp]
theorem coe_ne_infty (x : X) : (x : OnePoint X) â‰  âˆ :=
  nofun

@[simp]
theorem infty_ne_coe (x : X) : âˆ â‰  (x : OnePoint X) :=
  nofun

/-- Recursor for `OnePoint` using the preferred forms `âˆ` and `â†‘x`. -/
@[elab_as_elim, induction_eliminator, cases_eliminator]
protected def rec {C : OnePoint X â†’ Sort*} (infty : C âˆ) (coe : âˆ€ x : X, C x) :
    âˆ€ z : OnePoint X, C z
  | âˆ => infty
  | (x : X) => coe x

/-- An elimination principle for `OnePoint`. -/
@[inline] protected def elim : OnePoint X â†’ Y â†’ (X â†’ Y) â†’ Y := Option.elim

@[simp] theorem elim_infty (y : Y) (f : X â†’ Y) : âˆ.elim y f = y := rfl

@[simp] theorem elim_some (y : Y) (f : X â†’ Y) (x : X) : (some x).elim y f = f x := rfl

theorem isCompl_range_coe_infty : IsCompl (range ((â†‘) : X â†’ OnePoint X)) {âˆ} :=
  isCompl_range_some_none X

theorem range_coe_union_infty : range ((â†‘) : X â†’ OnePoint X) âˆª {âˆ} = univ :=
  range_some_union_none X

@[simp]
theorem insert_infty_range_coe : insert âˆ (range (@some X)) = univ :=
  insert_none_range_some _

@[simp]
theorem range_coe_inter_infty : range ((â†‘) : X â†’ OnePoint X) âˆ© {âˆ} = âˆ… :=
  range_some_inter_none X

@[simp]
theorem compl_range_coe : (range ((â†‘) : X â†’ OnePoint X))á¶œ = {âˆ} :=
  compl_range_some X

theorem compl_infty : ({âˆ}á¶œ : Set (OnePoint X)) = range ((â†‘) : X â†’ OnePoint X) :=
  (@isCompl_range_coe_infty X).symm.compl_eq

theorem compl_image_coe (s : Set X) : ((â†‘) '' s : Set (OnePoint X))á¶œ = (â†‘) '' sá¶œ âˆª {âˆ} := by
  rw [coe_injective.compl_image_eq, compl_range_coe]

theorem ne_infty_iff_exists {x : OnePoint X} : x â‰  âˆ â†” âˆƒ y : X, (y : OnePoint X) = x := by
  induction x using OnePoint.rec <;> simp

instance canLift : CanLift (OnePoint X) X (â†‘) fun x => x â‰  âˆ :=
  WithTop.canLift

theorem notMem_range_coe_iff {x : OnePoint X} : x âˆ‰ range some â†” x = âˆ := by
  rw [â† mem_compl_iff, compl_range_coe, mem_singleton_iff]

@[deprecated (since := "2025-05-23")] alias not_mem_range_coe_iff := notMem_range_coe_iff

theorem infty_notMem_range_coe : âˆ âˆ‰ range ((â†‘) : X â†’ OnePoint X) :=
  notMem_range_coe_iff.2 rfl

@[deprecated (since := "2025-05-23")] alias infty_not_mem_range_coe := infty_notMem_range_coe

theorem infty_notMem_image_coe {s : Set X} : âˆ âˆ‰ ((â†‘) : X â†’ OnePoint X) '' s :=
  notMem_subset (image_subset_range _ _) infty_notMem_range_coe

@[deprecated (since := "2025-05-23")] alias infty_not_mem_image_coe := infty_notMem_image_coe

@[simp]
theorem coe_preimage_infty : ((â†‘) : X â†’ OnePoint X) â»Â¹' {âˆ} = âˆ… := by
  ext
  simp

/-- Extend a map `f : X â†’ Y` to a map `OnePoint X â†’ OnePoint Y`
by sending infinity to infinity. -/
protected def map (f : X â†’ Y) : OnePoint X â†’ OnePoint Y :=
  Option.map f

@[simp] theorem map_infty (f : X â†’ Y) : OnePoint.map f âˆ = âˆ := rfl
@[simp] theorem map_some (f : X â†’ Y) (x : X) : (x : OnePoint X).map f = f x := rfl
@[simp] theorem map_id : OnePoint.map (id : X â†’ X) = id := Option.map_id

theorem map_comp {Z : Type*} (f : Y â†’ Z) (g : X â†’ Y) :
    OnePoint.map (f âˆ˜ g) = OnePoint.map f âˆ˜ OnePoint.map g :=
  (Option.map_comp_map _ _).symm

/-!
### Topological space structure on `OnePoint X`

We define a topological space structure on `OnePoint X` so that `s` is open if and only if

* `(â†‘) â»Â¹' s` is open in `X`;
* if `âˆ âˆˆ s`, then `((â†‘) â»Â¹' s)á¶œ` is compact.

Then we reformulate this definition in a few different ways, and prove that
`(â†‘) : X â†’ OnePoint X` is an open embedding. If `X` is not a compact space, then we also prove
that `(â†‘)` has dense range, so it is a dense embedding.
-/


variable [TopologicalSpace X]

instance : TopologicalSpace (OnePoint X) where
  IsOpen s := (âˆ âˆˆ s â†’ IsCompact (((â†‘) : X â†’ OnePoint X) â»Â¹' s)á¶œ) âˆ§
    IsOpen (((â†‘) : X â†’ OnePoint X) â»Â¹' s)
  isOpen_univ := by simp
  isOpen_inter s t := by
    rintro âŸ¨hms, hsâŸ© âŸ¨hmt, htâŸ©
    refine âŸ¨?_, hs.inter htâŸ©
    rintro âŸ¨hms', hmt'âŸ©
    simpa [compl_inter] using (hms hms').union (hmt hmt')
  isOpen_sUnion S ho := by
    suffices IsOpen ((â†‘) â»Â¹' â‹ƒâ‚€ S : Set X) by
      refine âŸ¨?_, thisâŸ©
      rintro âŸ¨s, hsS : s âˆˆ S, hs : âˆ âˆˆ sâŸ©
      refine IsCompact.of_isClosed_subset ((ho s hsS).1 hs) this.isClosed_compl ?_
      exact compl_subset_compl.mpr (preimage_mono <| subset_sUnion_of_mem hsS)
    rw [preimage_sUnion]
    exact isOpen_biUnion fun s hs => (ho s hs).2

variable {s : Set (OnePoint X)}

theorem isOpen_def :
    IsOpen s â†” (âˆ âˆˆ s â†’ IsCompact ((â†‘) â»Â¹' s : Set X)á¶œ) âˆ§ IsOpen ((â†‘) â»Â¹' s : Set X) :=
  Iff.rfl

theorem isOpen_iff_of_mem' (h : âˆ âˆˆ s) :
    IsOpen s â†” IsCompact ((â†‘) â»Â¹' s : Set X)á¶œ âˆ§ IsOpen ((â†‘) â»Â¹' s : Set X) := by
  simp [isOpen_def, h]

theorem isOpen_iff_of_mem (h : âˆ âˆˆ s) :
    IsOpen s â†” IsClosed ((â†‘) â»Â¹' s : Set X)á¶œ âˆ§ IsCompact ((â†‘) â»Â¹' s : Set X)á¶œ := by
  simp only [isOpen_iff_of_mem' h, isClosed_compl_iff, and_comm]

theorem isOpen_iff_of_notMem (h : âˆ âˆ‰ s) : IsOpen s â†” IsOpen ((â†‘) â»Â¹' s : Set X) := by
  simp [isOpen_def, h]

@[deprecated (since := "2025-05-23")] alias isOpen_iff_of_not_mem := isOpen_iff_of_notMem

theorem isClosed_iff_of_mem (h : âˆ âˆˆ s) : IsClosed s â†” IsClosed ((â†‘) â»Â¹' s : Set X) := by
  have : âˆ âˆ‰ sá¶œ := fun H => H h
  rw [â† isOpen_compl_iff, isOpen_iff_of_notMem this, â† isOpen_compl_iff, preimage_compl]

theorem isClosed_iff_of_notMem (h : âˆ âˆ‰ s) :
    IsClosed s â†” IsClosed ((â†‘) â»Â¹' s : Set X) âˆ§ IsCompact ((â†‘) â»Â¹' s : Set X) := by
  rw [â† isOpen_compl_iff, isOpen_iff_of_mem (mem_compl h), â† preimage_compl, compl_compl]

@[deprecated (since := "2025-05-23")] alias isClosed_iff_of_not_mem := isClosed_iff_of_notMem

@[simp]
theorem isOpen_image_coe {s : Set X} : IsOpen ((â†‘) '' s : Set (OnePoint X)) â†” IsOpen s := by
  rw [isOpen_iff_of_notMem infty_notMem_image_coe, preimage_image_eq _ coe_injective]

theorem isOpen_compl_image_coe {s : Set X} :
    IsOpen ((â†‘) '' s : Set (OnePoint X))á¶œ â†” IsClosed s âˆ§ IsCompact s := by
  rw [isOpen_iff_of_mem, â† preimage_compl, compl_compl, preimage_image_eq _ coe_injective]
  exact infty_notMem_image_coe

@[simp]
theorem isClosed_image_coe {s : Set X} :
    IsClosed ((â†‘) '' s : Set (OnePoint X)) â†” IsClosed s âˆ§ IsCompact s := by
  rw [â† isOpen_compl_iff, isOpen_compl_image_coe]

/-- An open set in `OnePoint X` constructed from a closed compact set in `X` -/
def opensOfCompl (s : Set X) (hâ‚ : IsClosed s) (hâ‚‚ : IsCompact s) :
    TopologicalSpace.Opens (OnePoint X) :=
  âŸ¨((â†‘) '' s)á¶œ, isOpen_compl_image_coe.2 âŸ¨hâ‚, hâ‚‚âŸ©âŸ©

theorem infty_mem_opensOfCompl {s : Set X} (hâ‚ : IsClosed s) (hâ‚‚ : IsCompact s) :
    âˆ âˆˆ opensOfCompl s hâ‚ hâ‚‚ :=
  mem_compl infty_notMem_image_coe

@[continuity]
theorem continuous_coe : Continuous ((â†‘) : X â†’ OnePoint X) :=
  continuous_def.mpr fun _s hs => hs.right

theorem isOpenMap_coe : IsOpenMap ((â†‘) : X â†’ OnePoint X) := fun _ => isOpen_image_coe.2

theorem isOpenEmbedding_coe : IsOpenEmbedding ((â†‘) : X â†’ OnePoint X) :=
  .of_continuous_injective_isOpenMap continuous_coe coe_injective isOpenMap_coe

theorem isOpen_range_coe : IsOpen (range ((â†‘) : X â†’ OnePoint X)) :=
  isOpenEmbedding_coe.isOpen_range

theorem isClosed_infty : IsClosed ({âˆ} : Set (OnePoint X)) := by
  rw [â† compl_range_coe, isClosed_compl_iff]
  exact isOpen_range_coe

theorem nhds_coe_eq (x : X) : ğ“ â†‘x = map ((â†‘) : X â†’ OnePoint X) (ğ“ x) :=
  (isOpenEmbedding_coe.map_nhds_eq x).symm

theorem nhdsWithin_coe_image (s : Set X) (x : X) :
    ğ“[(â†‘) '' s] (x : OnePoint X) = map (â†‘) (ğ“[s] x) :=
  (isOpenEmbedding_coe.isEmbedding.map_nhdsWithin_eq _ _).symm

theorem nhdsWithin_coe (s : Set (OnePoint X)) (x : X) : ğ“[s] â†‘x = map (â†‘) (ğ“[(â†‘) â»Â¹' s] x) :=
  (isOpenEmbedding_coe.map_nhdsWithin_preimage_eq _ _).symm

theorem comap_coe_nhds (x : X) : comap ((â†‘) : X â†’ OnePoint X) (ğ“ x) = ğ“ x :=
  (isOpenEmbedding_coe.isInducing.nhds_eq_comap x).symm

/-- If `x` is not an isolated point of `X`, then `x : OnePoint X` is not an isolated point
of `OnePoint X`. -/
instance nhdsNE_coe_neBot (x : X) [h : NeBot (ğ“[â‰ ] x)] : NeBot (ğ“[â‰ ] (x : OnePoint X)) := by
  simpa [nhdsWithin_coe, preimage, coe_eq_coe] using h.map some

theorem nhdsNE_infty_eq : ğ“[â‰ ] (âˆ : OnePoint X) = map (â†‘) (coclosedCompact X) := by
  refine (nhdsWithin_basis_open âˆ _).ext (hasBasis_coclosedCompact.map _) ?_ ?_
  Â· rintro s âŸ¨hs, hsoâŸ©
    refine âŸ¨_, (isOpen_iff_of_mem hs).mp hso, ?_âŸ©
    simp
  Â· rintro s âŸ¨hâ‚, hâ‚‚âŸ©
    refine âŸ¨_, âŸ¨mem_compl infty_notMem_image_coe, isOpen_compl_image_coe.2 âŸ¨hâ‚, hâ‚‚âŸ©âŸ©, ?_âŸ©
    simp [compl_image_coe, â† diff_eq]

/-- If `X` is a non-compact space, then `âˆ` is not an isolated point of `OnePoint X`. -/
instance nhdsNE_infty_neBot [NoncompactSpace X] : NeBot (ğ“[â‰ ] (âˆ : OnePoint X)) := by
  rw [nhdsNE_infty_eq]
  infer_instance

instance (priority := 900) nhdsNE_neBot [âˆ€ x : X, NeBot (ğ“[â‰ ] x)] [NoncompactSpace X]
    (x : OnePoint X) : NeBot (ğ“[â‰ ] x) :=
  OnePoint.rec OnePoint.nhdsNE_infty_neBot (fun y => OnePoint.nhdsNE_coe_neBot y) x

theorem nhds_infty_eq : ğ“ (âˆ : OnePoint X) = map (â†‘) (coclosedCompact X) âŠ” pure âˆ := by
  rw [â† nhdsNE_infty_eq, nhdsNE_sup_pure]

theorem tendsto_coe_infty : Tendsto (â†‘) (coclosedCompact X) (ğ“ (âˆ : OnePoint X)) := by
  rw [nhds_infty_eq]
  exact Filter.Tendsto.mono_right tendsto_map le_sup_left

theorem hasBasis_nhds_infty :
    (ğ“ (âˆ : OnePoint X)).HasBasis (fun s : Set X => IsClosed s âˆ§ IsCompact s) fun s =>
      (â†‘) '' sá¶œ âˆª {âˆ} := by
  rw [nhds_infty_eq]
  exact (hasBasis_coclosedCompact.map _).sup_pure _

@[simp]
theorem comap_coe_nhds_infty : comap ((â†‘) : X â†’ OnePoint X) (ğ“ âˆ) = coclosedCompact X := by
  simp [nhds_infty_eq, comap_sup, comap_map coe_injective]

theorem le_nhds_infty {f : Filter (OnePoint X)} :
    f â‰¤ ğ“ âˆ â†” âˆ€ s : Set X, IsClosed s â†’ IsCompact s â†’ (â†‘) '' sá¶œ âˆª {âˆ} âˆˆ f := by
  simp only [hasBasis_nhds_infty.ge_iff, and_imp]

theorem ultrafilter_le_nhds_infty {f : Ultrafilter (OnePoint X)} :
    (f : Filter (OnePoint X)) â‰¤ ğ“ âˆ â†” âˆ€ s : Set X, IsClosed s â†’ IsCompact s â†’ (â†‘) '' s âˆ‰ f := by
  simp only [le_nhds_infty, â† compl_image_coe, Ultrafilter.mem_coe,
    Ultrafilter.compl_mem_iff_notMem]

theorem tendsto_nhds_infty' {Î± : Type*} {f : OnePoint X â†’ Î±} {l : Filter Î±} :
    Tendsto f (ğ“ âˆ) l â†” Tendsto f (pure âˆ) l âˆ§ Tendsto (f âˆ˜ (â†‘)) (coclosedCompact X) l := by
  simp [nhds_infty_eq, and_comm]

theorem tendsto_nhds_infty {Î± : Type*} {f : OnePoint X â†’ Î±} {l : Filter Î±} :
    Tendsto f (ğ“ âˆ) l â†”
      âˆ€ s âˆˆ l, f âˆ âˆˆ s âˆ§ âˆƒ t : Set X, IsClosed t âˆ§ IsCompact t âˆ§ MapsTo (f âˆ˜ (â†‘)) tá¶œ s :=
  tendsto_nhds_infty'.trans <| by
    simp only [tendsto_pure_left, hasBasis_coclosedCompact.tendsto_left_iff, forall_and,
      and_assoc]

theorem continuousAt_infty' {Y : Type*} [TopologicalSpace Y] {f : OnePoint X â†’ Y} :
    ContinuousAt f âˆ â†” Tendsto (f âˆ˜ (â†‘)) (coclosedCompact X) (ğ“ (f âˆ)) :=
  tendsto_nhds_infty'.trans <| and_iff_right (tendsto_pure_nhds _ _)

theorem continuousAt_infty {Y : Type*} [TopologicalSpace Y] {f : OnePoint X â†’ Y} :
    ContinuousAt f âˆ â†”
      âˆ€ s âˆˆ ğ“ (f âˆ), âˆƒ t : Set X, IsClosed t âˆ§ IsCompact t âˆ§ MapsTo (f âˆ˜ (â†‘)) tá¶œ s :=
  continuousAt_infty'.trans <| by simp only [hasBasis_coclosedCompact.tendsto_left_iff, and_assoc]

theorem continuousAt_coe {Y : Type*} [TopologicalSpace Y] {f : OnePoint X â†’ Y} {x : X} :
    ContinuousAt f x â†” ContinuousAt (f âˆ˜ (â†‘)) x := by
  rw [ContinuousAt, nhds_coe_eq, tendsto_map'_iff, ContinuousAt]; rfl

lemma continuous_iff {Y : Type*} [TopologicalSpace Y] (f : OnePoint X â†’ Y) : Continuous f â†”
    Tendsto (fun x : X â†¦ f x) (coclosedCompact X) (ğ“ (f âˆ)) âˆ§ Continuous (fun x : X â†¦ f x) := by
  simp only [continuous_iff_continuousAt, OnePoint.forall, continuousAt_coe, continuousAt_infty',
    Function.comp_def]

/--
A constructor for continuous maps out of a one point compactification, given a continuous map from
the underlying space and a limit value at infinity.
-/
def continuousMapMk {Y : Type*} [TopologicalSpace Y] (f : C(X, Y)) (y : Y)
    (h : Tendsto f (coclosedCompact X) (ğ“ y)) : C(OnePoint X, Y) where
  toFun x := x.elim y f
  continuous_toFun := by
    rw [continuous_iff]
    refine âŸ¨h, map_continuous fâŸ©

lemma continuous_iff_from_discrete {Y : Type*} [TopologicalSpace Y]
    [DiscreteTopology X] (f : OnePoint X â†’ Y) :
    Continuous f â†” Tendsto (fun x : X â†¦ f x) cofinite (ğ“ (f âˆ)) := by
  simp [continuous_iff, cocompact_eq_cofinite, continuous_of_discreteTopology]

/--
A constructor for continuous maps out of a one point compactification of a discrete space, given a
map from the underlying space and a limit value at infinity.
-/
def continuousMapMkDiscrete {Y : Type*} [TopologicalSpace Y]
    [DiscreteTopology X] (f : X â†’ Y) (y : Y) (h : Tendsto f cofinite (ğ“ y)) :
    C(OnePoint X, Y) :=
  continuousMapMk âŸ¨f, continuous_of_discreteTopologyâŸ© y (by simpa [cocompact_eq_cofinite])

variable (X) in
/--
Continuous maps out of the one point compactification of an infinite discrete space to a Hausdorff
space correspond bijectively to "convergent" maps out of the discrete space.
-/
noncomputable def continuousMapDiscreteEquiv (Y : Type*) [DiscreteTopology X] [TopologicalSpace Y]
    [T2Space Y] [Infinite X] :
    C(OnePoint X, Y) â‰ƒ { f : X â†’ Y // âˆƒ L, Tendsto (fun x : X â†¦ f x) cofinite (ğ“ L) } where
  toFun f := âŸ¨(f Â·), âŸ¨f âˆ, continuous_iff_from_discrete _ |>.mp (map_continuous f)âŸ©âŸ©
  invFun f :=
    { toFun := fun x => match x with
        | âˆ => Classical.choose f.2
        | some x => f.1 x
      continuous_toFun := continuous_iff_from_discrete _ |>.mpr <| Classical.choose_spec f.2 }
  left_inv f := by
    ext x
    refine OnePoint.rec ?_ ?_ x
    Â· refine tendsto_nhds_unique ?_ (continuous_iff_from_discrete _ |>.mp <| map_continuous f)
      let f' : { f : X â†’ Y // âˆƒ L, Tendsto (fun x : X â†¦ f x) cofinite (ğ“ L) } :=
        âŸ¨fun x â†¦ f x, âŸ¨f âˆ, continuous_iff_from_discrete f |>.mp <| map_continuous fâŸ©âŸ©
      exact Classical.choose_spec f'.property
    Â· simp

lemma continuous_iff_from_nat {Y : Type*} [TopologicalSpace Y] (f : OnePoint â„• â†’ Y) :
    Continuous f â†” Tendsto (fun x : â„• â†¦ f x) atTop (ğ“ (f âˆ)) := by
  rw [continuous_iff_from_discrete, Nat.cofinite_eq_atTop]

/--
A constructor for continuous maps out of the one point compactification of `â„•`, given a
sequence and a limit value at infinity.
-/
def continuousMapMkNat {Y : Type*} [TopologicalSpace Y]
    (f : â„• â†’ Y) (y : Y) (h : Tendsto f atTop (ğ“ y)) :
    C(OnePoint â„•, Y) :=
  continuousMapMkDiscrete f y (by rwa [Nat.cofinite_eq_atTop])

/--
Continuous maps out of the one point compactification of `â„•` to a Hausdorff space `Y` correspond
bijectively to convergent sequences in `Y`.
-/
noncomputable def continuousMapNatEquiv (Y : Type*) [TopologicalSpace Y] [T2Space Y] :
    C(OnePoint â„•, Y) â‰ƒ { f : â„• â†’ Y // âˆƒ L, Tendsto (f Â·) atTop (ğ“ L) } := by
  refine (continuousMapDiscreteEquiv â„• Y).trans {
    toFun := fun âŸ¨f, hfâŸ© â†¦ âŸ¨f, by rwa [â† Nat.cofinite_eq_atTop]âŸ©
    invFun := fun âŸ¨f, hfâŸ© â†¦ âŸ¨f, by rwa [Nat.cofinite_eq_atTop]âŸ© }

/-- If `X` is not a compact space, then the natural embedding `X â†’ OnePoint X` has dense range.
-/
theorem denseRange_coe [NoncompactSpace X] : DenseRange ((â†‘) : X â†’ OnePoint X) := by
  rw [DenseRange, â† compl_infty]
  exact dense_compl_singleton _

theorem isDenseEmbedding_coe [NoncompactSpace X] : IsDenseEmbedding ((â†‘) : X â†’ OnePoint X) :=
  { isOpenEmbedding_coe with dense := denseRange_coe }

@[simp, norm_cast]
theorem specializes_coe {x y : X} : (x : OnePoint X) â¤³ y â†” x â¤³ y :=
  isOpenEmbedding_coe.isInducing.specializes_iff

@[simp, norm_cast]
theorem inseparable_coe {x y : X} : Inseparable (x : OnePoint X) y â†” Inseparable x y :=
  isOpenEmbedding_coe.isInducing.inseparable_iff

theorem not_specializes_infty_coe {x : X} : Â¬Specializes âˆ (x : OnePoint X) :=
  isClosed_infty.not_specializes rfl (coe_ne_infty x)

theorem not_inseparable_infty_coe {x : X} : Â¬Inseparable âˆ (x : OnePoint X) := fun h =>
  not_specializes_infty_coe h.specializes

theorem not_inseparable_coe_infty {x : X} : Â¬Inseparable (x : OnePoint X) âˆ := fun h =>
  not_specializes_infty_coe h.specializes'

theorem inseparable_iff {x y : OnePoint X} :
    Inseparable x y â†” x = âˆ âˆ§ y = âˆ âˆ¨ âˆƒ x' : X, x = x' âˆ§ âˆƒ y' : X, y = y' âˆ§ Inseparable x' y' := by
  induction x using OnePoint.rec <;> induction y using OnePoint.rec <;>
    simp [not_inseparable_infty_coe, not_inseparable_coe_infty, coe_eq_coe, Inseparable.refl]

theorem continuous_map_iff [TopologicalSpace Y] {f : X â†’ Y} :
    Continuous (OnePoint.map f) â†”
      Continuous f âˆ§ Tendsto f (coclosedCompact X) (coclosedCompact Y) := by
  simp_rw [continuous_iff, map_some, â† comap_coe_nhds_infty, tendsto_comap_iff, map_infty,
    isOpenEmbedding_coe.isInducing.continuous_iff (Y := Y)]
  exact and_comm

theorem continuous_map [TopologicalSpace Y] {f : X â†’ Y} (hc : Continuous f)
    (h : Tendsto f (coclosedCompact X) (coclosedCompact Y)) :
    Continuous (OnePoint.map f) :=
  continuous_map_iff.mpr âŸ¨hc, hâŸ©

/-!
### Compactness and separation properties

In this section we prove that `OnePoint X` is a compact space; it is a Tâ‚€ (resp., Tâ‚) space if
the original space satisfies the same separation axiom. If the original space is a locally compact
Hausdorff space, then `OnePoint X` is a normal (hence, Tâ‚ƒ and Hausdorff) space.

Finally, if the original space `X` is *not* compact and is a preconnected space, then
`OnePoint X` is a connected space.
-/

/-- For any topological space `X`, its one point compactification is a compact space. -/
instance : CompactSpace (OnePoint X) where
  isCompact_univ := by
    have : Tendsto ((â†‘) : X â†’ OnePoint X) (cocompact X) (ğ“ âˆ) := by
      rw [nhds_infty_eq]
      exact (tendsto_map.mono_left cocompact_le_coclosedCompact).mono_right le_sup_left
    rw [â† insert_none_range_some X]
    exact this.isCompact_insert_range_of_cocompact continuous_coe

/-- The one point compactification of a `T0Space` space is a `T0Space`. -/
instance [T0Space X] : T0Space (OnePoint X) := by
  refine âŸ¨fun x y hxy => ?_âŸ©
  rcases inseparable_iff.1 hxy with (âŸ¨rfl, rflâŸ© | âŸ¨x, rfl, y, rfl, hâŸ©)
  exacts [rfl, congr_arg some h.eq]

/-- The one point compactification of a `T1Space` space is a `T1Space`. -/
instance [T1Space X] : T1Space (OnePoint X) where
  t1 z := by
    induction z using OnePoint.rec
    Â· exact isClosed_infty
    Â· rw [â† image_singleton, isClosed_image_coe]
      exact âŸ¨isClosed_singleton, isCompact_singletonâŸ©

/-- The one point compactification of a weakly locally compact Râ‚ space
is a normal topological space. -/
instance [WeaklyLocallyCompactSpace X] [R1Space X] : NormalSpace (OnePoint X) := by
  suffices R1Space (OnePoint X) by infer_instance
  have key : âˆ€ z : X, Disjoint (ğ“ (some z)) (ğ“ âˆ) := fun z â†¦ by
    rw [nhds_infty_eq, disjoint_sup_right, nhds_coe_eq, coclosedCompact_eq_cocompact,
      disjoint_map coe_injective, â† principal_singleton, disjoint_principal_right, compl_infty]
    exact âŸ¨disjoint_nhds_cocompact z, range_mem_mapâŸ©
  refine âŸ¨fun x y â†¦ ?_âŸ©
  induction x using OnePoint.rec <;> induction y using OnePoint.rec
  Â· exact .inl le_rfl
  Â· exact .inr (key _).symm
  Â· exact .inr (key _)
  Â· rw [nhds_coe_eq, nhds_coe_eq, disjoint_map coe_injective, specializes_coe]
    apply specializes_or_disjoint_nhds

/-- The one point compactification of a weakly locally compact Hausdorff space is a Tâ‚„
(hence, Hausdorff and regular) topological space. -/
example [WeaklyLocallyCompactSpace X] [T2Space X] : T4Space (OnePoint X) := inferInstance

/-- If `X` is not a compact space, then `OnePoint X` is a connected space. -/
instance [PreconnectedSpace X] [NoncompactSpace X] : ConnectedSpace (OnePoint X) where
  toPreconnectedSpace := isDenseEmbedding_coe.isDenseInducing.preconnectedSpace
  toNonempty := inferInstance

/-- If `X` is an infinite type with discrete topology (e.g., `â„•`), then the identity map from
`CofiniteTopology (OnePoint X)` to `OnePoint X` is not continuous. -/
theorem not_continuous_cofiniteTopology_of_symm [Infinite X] [DiscreteTopology X] :
    Â¬Continuous (@CofiniteTopology.of (OnePoint X)).symm := by
  inhabit X
  simp only [continuous_iff_continuousAt, ContinuousAt, not_forall]
  use CofiniteTopology.of â†‘(default : X)
  simpa [nhds_coe_eq, nhds_discrete, CofiniteTopology.nhds_eq] using
    (finite_singleton ((default : X) : OnePoint X)).infinite_compl

instance (X : Type*) [TopologicalSpace X] [DiscreteTopology X] :
    TotallySeparatedSpace (OnePoint X) where
  isTotallySeparated_univ x _ y _ hxy := by
    cases x with
    | infty =>
      refine âŸ¨{y}á¶œ, {y}, isOpen_compl_singleton, ?_, hxy, rfl, (compl_union_self _).symm.subset,
        disjoint_compl_leftâŸ©
      rw [OnePoint.isOpen_iff_of_notMem]
      exacts [isOpen_discrete _, hxy]
    | coe val =>
      refine âŸ¨{some val}, {some val}á¶œ, ?_, isOpen_compl_singleton, rfl, hxy.symm, by simp,
        disjoint_compl_rightâŸ©
      rw [OnePoint.isOpen_iff_of_notMem]
      exacts [isOpen_discrete _, (Option.some_ne_none val).symm]

section Uniqueness

variable [TopologicalSpace Y] [T2Space Y] [CompactSpace Y]
  (y : Y) (f : X â†’ Y) (hf : IsEmbedding f) (hy : range f = {y}á¶œ)

open scoped Classical in
/-- If `f` embeds `X` into a compact Hausdorff space `Y`, and has exactly one point outside its
range, then `(Y, f)` is the one-point compactification of `X`. -/
noncomputable def equivOfIsEmbeddingOfRangeEq :
    OnePoint X â‰ƒâ‚œ Y :=
  have _i := hf.t2Space
  have : Tendsto f (coclosedCompact X) (ğ“ y) := by
    rw [coclosedCompact_eq_cocompact, hasBasis_cocompact.tendsto_left_iff]
    intro N hN
    obtain âŸ¨U, hUâ‚, hUâ‚‚, hUâ‚ƒâŸ© := mem_nhds_iff.mp hN
    refine âŸ¨fâ»Â¹' Uá¶œ, ?_, by simpa using (mapsTo_preimage f U).mono_right hUâ‚âŸ©
    rw [hf.isCompact_iff, image_preimage_eq_iff.mpr (by simpa [hy])]
    exact (isClosed_compl_iff.mpr hUâ‚‚).isCompact
  let e : OnePoint X â‰ƒ Y :=
    { toFun := fun p â†¦ p.elim y f
      invFun := fun q â†¦ if hq : q = y then âˆ else â†‘(show q âˆˆ range f from by simpa [hy]).choose
      left_inv := fun p â†¦ by
        induction p using OnePoint.rec with
        | infty => simp
        | coe p =>
          have hp : f p â‰  y := by simpa [hy] using mem_range_self (f := f) p
          simpa [hp] using hf.injective (mem_range_self p).choose_spec
      right_inv := fun q â†¦ by
        rcases eq_or_ne q y with rfl | hq
        Â· simp
        Â· have hq' : q âˆˆ range f := by simpa [hy]
          simpa [hq] using hq'.choose_spec }
  Continuous.homeoOfEquivCompactToT2 <| (continuous_iff e).mpr âŸ¨this, hf.continuousâŸ©

@[simp]
lemma equivOfIsEmbeddingOfRangeEq_apply_coe (x : X) :
    equivOfIsEmbeddingOfRangeEq y f hf hy x = f x :=
  rfl

@[simp]
lemma equivOfIsEmbeddingOfRangeEq_apply_infty :
    equivOfIsEmbeddingOfRangeEq y f hf hy âˆ = y :=
  rfl

end Uniqueness

end OnePoint

namespace Homeomorph

variable [TopologicalSpace X] [TopologicalSpace Y]

open OnePoint

/-- Extend a homeomorphism of topological spaces
to the homeomorphism of their one point compactifications. -/
@[simps]
def onePointCongr (h : X â‰ƒâ‚œ Y) : OnePoint X â‰ƒâ‚œ OnePoint Y where
  __ := h.toEquiv.withTopCongr
  toFun := OnePoint.map h
  invFun := OnePoint.map h.symm
  continuous_toFun := continuous_map (map_continuous h) h.map_coclosedCompact.le
  continuous_invFun := continuous_map (map_continuous h.symm) h.symm.map_coclosedCompact.le

end Homeomorph

/-- A concrete counterexample shows that `Continuous.homeoOfEquivCompactToT2`
cannot be generalized from `T2Space` to `T1Space`.

Let `Î± = OnePoint â„•` be the one-point compactification of `â„•`, and let `Î²` be the same space
`OnePoint â„•` with the cofinite topology.  Then `Î±` is compact, `Î²` is T1, and the identity map
`id : Î± â†’ Î²` is a continuous equivalence that is not a homeomorphism.
-/
theorem Continuous.homeoOfEquivCompactToT2.t1_counterexample :
    âˆƒ (Î± Î² : Type) (_ : TopologicalSpace Î±) (_ : TopologicalSpace Î²),
      CompactSpace Î± âˆ§ T1Space Î² âˆ§ âˆƒ f : Î± â‰ƒ Î², Continuous f âˆ§ Â¬Continuous f.symm :=
  âŸ¨OnePoint â„•, CofiniteTopology (OnePoint â„•), inferInstance, inferInstance, inferInstance,
    inferInstance, CofiniteTopology.of, CofiniteTopology.continuous_of,
    OnePoint.not_continuous_cofiniteTopology_of_symmâŸ©
