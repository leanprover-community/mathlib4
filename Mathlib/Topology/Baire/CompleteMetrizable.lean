/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
import Mathlib.Analysis.SpecificLimits.Basic
import Mathlib.Topology.Metrizable.Uniformity

#align_import topology.metric_space.baire from "leanprover-community/mathlib"@"b9e46fe101fc897fb2e7edaf0bf1f09ea49eb81a"

/-!
# First Baire theorem

In this file we prove that a completely metrizable topological space is a Baire space.
Since `Mathlib` does not have the notion of a completely metrizable topological space yet,
we state it for a complete uniform space with countably generated uniformity filter.
-/

open Filter EMetric Set
open scoped Topology Uniformity ENNReal

variable {X : Type*} [UniformSpace X] [CompleteSpace X] [(ð“¤ X).IsCountablyGenerated]

/-- **First Baire theorem**: a completely metrizable topological space has Baire property.

Since `Mathlib` does not have the notion of a completely metrizable topological space yet,
we state it for a complete uniform space with countably generated uniformity filter. -/
instance (priority := 100) BaireSpace.of_pseudoEMetricSpace_completeSpace : BaireSpace X := by
  let _ := UniformSpace.pseudoMetricSpace X
  refine âŸ¨fun f ho hd => ?_âŸ©
  let B : â„• â†’ â„â‰¥0âˆž := fun n => 1 / 2 ^ n
  have Bpos : âˆ€ n, 0 < B n := fun n â†¦
    ENNReal.div_pos one_ne_zero <| ENNReal.pow_ne_top ENNReal.coe_ne_top
  /- Translate the density assumption into two functions `center` and `radius` associating
    to any n, x, Î´, Î´pos a center and a positive radius such that
    `closedBall center radius` is included both in `f n` and in `closedBall x Î´`.
    We can also require `radius â‰¤ (1/2)^(n+1)`, to ensure we get a Cauchy sequence later. -/
  have : âˆ€ n x Î´, Î´ â‰  0 â†’ âˆƒ y r, 0 < r âˆ§ r â‰¤ B (n + 1) âˆ§ closedBall y r âŠ† closedBall x Î´ âˆ© f n := by
    intro n x Î´ Î´pos
    have : x âˆˆ closure (f n) := hd n x
    rcases EMetric.mem_closure_iff.1 this (Î´ / 2) (ENNReal.half_pos Î´pos) with âŸ¨y, ys, xyâŸ©
    rw [edist_comm] at xy
    obtain âŸ¨r, rpos, hrâŸ© : âˆƒ r > 0, closedBall y r âŠ† f n :=
      nhds_basis_closed_eball.mem_iff.1 (isOpen_iff_mem_nhds.1 (ho n) y ys)
    refine âŸ¨y, min (min (Î´ / 2) r) (B (n + 1)), ?_, ?_, fun z hz => âŸ¨?_, ?_âŸ©âŸ©
    Â· show 0 < min (min (Î´ / 2) r) (B (n + 1))
      exact lt_min (lt_min (ENNReal.half_pos Î´pos) rpos) (Bpos (n + 1))
    Â· show min (min (Î´ / 2) r) (B (n + 1)) â‰¤ B (n + 1)
      exact min_le_right _ _
    Â· show z âˆˆ closedBall x Î´
      calc
        edist z x â‰¤ edist z y + edist y x := edist_triangle _ _ _
        _ â‰¤ min (min (Î´ / 2) r) (B (n + 1)) + Î´ / 2 := add_le_add hz (le_of_lt xy)
        _ â‰¤ Î´ / 2 + Î´ / 2 := (add_le_add (le_trans (min_le_left _ _) (min_le_left _ _)) le_rfl)
        _ = Î´ := ENNReal.add_halves Î´
    show z âˆˆ f n
    exact hr (calc
      edist z y â‰¤ min (min (Î´ / 2) r) (B (n + 1)) := hz
      _ â‰¤ r := le_trans (min_le_left _ _) (min_le_right _ _))
  choose! center radius Hpos HB Hball using this
  refine fun x => (mem_closure_iff_nhds_basis nhds_basis_closed_eball).2 fun Îµ Îµpos => ?_
  /- `Îµ` is positive. We have to find a point in the ball of radius `Îµ` around `x` belonging to all
    `f n`. For this, we construct inductively a sequence `F n = (c n, r n)` such that the closed
    ball `closedBall (c n) (r n)` is included in the previous ball and in `f n`, and such that
    `r n` is small enough to ensure that `c n` is a Cauchy sequence. Then `c n` converges to a
    limit which belongs to all the `f n`. -/
  let F : â„• â†’ X Ã— â„â‰¥0âˆž := fun n =>
    Nat.recOn n (Prod.mk x (min Îµ (B 0))) fun n p => Prod.mk (center n p.1 p.2) (radius n p.1 p.2)
  let c : â„• â†’ X := fun n => (F n).1
  let r : â„• â†’ â„â‰¥0âˆž := fun n => (F n).2
  have rpos : âˆ€ n, 0 < r n := by
    intro n
    induction n with
    | zero => exact lt_min Îµpos (Bpos 0)
    | succ n hn => exact Hpos n (c n) (r n) hn.ne'
  have r0 : âˆ€ n, r n â‰  0 := fun n => (rpos n).ne'
  have rB : âˆ€ n, r n â‰¤ B n := by
    intro n
    cases n with
    | zero => exact min_le_right _ _
    | succ n => exact HB n (c n) (r n) (r0 n)
  have incl : âˆ€ n, closedBall (c (n + 1)) (r (n + 1)) âŠ† closedBall (c n) (r n) âˆ© f n :=
    fun n => Hball n (c n) (r n) (r0 n)
  have cdist : âˆ€ n, edist (c n) (c (n + 1)) â‰¤ B n := by
    intro n
    rw [edist_comm]
    have A : c (n + 1) âˆˆ closedBall (c (n + 1)) (r (n + 1)) := mem_closedBall_self
    have I :=
      calc
        closedBall (c (n + 1)) (r (n + 1)) âŠ† closedBall (c n) (r n) :=
          Subset.trans (incl n) inter_subset_left
        _ âŠ† closedBall (c n) (B n) := closedBall_subset_closedBall (rB n)
    exact I A
  have : CauchySeq c := cauchySeq_of_edist_le_geometric_two _ ENNReal.one_ne_top cdist
  -- as the sequence `c n` is Cauchy in a complete space, it converges to a limit `y`.
  rcases cauchySeq_tendsto_of_complete this with âŸ¨y, ylimâŸ©
  -- this point `y` will be the desired point. We will check that it belongs to all
  -- `f n` and to `ball x Îµ`.
  use y
  simp only [exists_prop, Set.mem_iInter]
  have I : âˆ€ n, âˆ€ m â‰¥ n, closedBall (c m) (r m) âŠ† closedBall (c n) (r n) := by
    intro n
    refine Nat.le_induction ?_ fun m _ h => ?_
    Â· exact Subset.refl _
    Â· exact Subset.trans (incl m) (Subset.trans inter_subset_left h)
  have yball : âˆ€ n, y âˆˆ closedBall (c n) (r n) := by
    intro n
    refine isClosed_ball.mem_of_tendsto ylim ?_
    refine (Filter.eventually_ge_atTop n).mono fun m hm => ?_
    exact I n m hm mem_closedBall_self
  constructor
  Â· show âˆ€ n, y âˆˆ f n
    intro n
    have : closedBall (c (n + 1)) (r (n + 1)) âŠ† f n :=
      Subset.trans (incl n) inter_subset_right
    exact this (yball (n + 1))
  show edist y x â‰¤ Îµ
  exact le_trans (yball 0) (min_le_left _ _)
#align baire_category_theorem_emetric_complete BaireSpace.of_pseudoEMetricSpace_completeSpace
