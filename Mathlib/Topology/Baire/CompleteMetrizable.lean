/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Yury Kudryashov
-/
import Mathlib.Analysis.SpecificLimits.Basic
import Mathlib.Tactic.Finiteness
import Mathlib.Topology.Metrizable.Uniformity

/-!
# First Baire theorem

In this file we prove that a completely metrizable topological space is a Baire space.
Since `Mathlib` does not have the notion of a completely metrizable topological space yet,
we state it for a complete uniform space with countably generated uniformity filter.
-/

open Filter EMetric Set
open scoped Topology Uniformity ENNReal

variable {X : Type*} [UniformSpace X] [CompleteSpace X] [(ùì§ X).IsCountablyGenerated]

/-- **First Baire theorem**: a completely metrizable topological space has Baire property.

Since `Mathlib` does not have the notion of a completely metrizable topological space yet,
we state it for a complete uniform space with countably generated uniformity filter. -/
instance (priority := 100) BaireSpace.of_pseudoEMetricSpace_completeSpace : BaireSpace X := by
  let _ := UniformSpace.pseudoMetricSpace X
  refine ‚ü®fun f ho hd ‚Ü¶ ?_‚ü©
  let B : ‚Ñï ‚Üí ‚Ñù‚â•0‚àû := fun n ‚Ü¶ 1 / 2 ^ n
  have Bpos : ‚àÄ n, 0 < B n := fun n ‚Ü¶
    ENNReal.div_pos one_ne_zero <| by finiteness
  /- Translate the density assumption into two functions `center` and `radius` associating
    to any n, x, Œ¥, Œ¥pos a center and a positive radius such that
    `closedBall center radius` is included both in `f n` and in `closedBall x Œ¥`.
    We can also require `radius ‚â§ (1/2)^(n+1)`, to ensure we get a Cauchy sequence later. -/
  have : ‚àÄ n x Œ¥, Œ¥ ‚â† 0 ‚Üí ‚àÉ y r, 0 < r ‚àß r ‚â§ B (n + 1) ‚àß closedBall y r ‚äÜ closedBall x Œ¥ ‚à© f n := by
    intro n x Œ¥ Œ¥pos
    have : x ‚àà closure (f n) := hd n x
    rcases EMetric.mem_closure_iff.1 this (Œ¥ / 2) (ENNReal.half_pos Œ¥pos) with ‚ü®y, ys, xy‚ü©
    rw [edist_comm] at xy
    obtain ‚ü®r, rpos, hr‚ü© : ‚àÉ r > 0, closedBall y r ‚äÜ f n :=
      nhds_basis_closed_eball.mem_iff.1 (isOpen_iff_mem_nhds.1 (ho n) y ys)
    refine ‚ü®y, min (min (Œ¥ / 2) r) (B (n + 1)), ?_, ?_, fun z hz ‚Ü¶ ‚ü®?_, ?_‚ü©‚ü©
    ¬∑ show 0 < min (min (Œ¥ / 2) r) (B (n + 1))
      exact lt_min (lt_min (ENNReal.half_pos Œ¥pos) rpos) (Bpos (n + 1))
    ¬∑ show min (min (Œ¥ / 2) r) (B (n + 1)) ‚â§ B (n + 1)
      exact min_le_right _ _
    ¬∑ show z ‚àà closedBall x Œ¥
      calc
        edist z x ‚â§ edist z y + edist y x := edist_triangle _ _ _
        _ ‚â§ min (min (Œ¥ / 2) r) (B (n + 1)) + Œ¥ / 2 := add_le_add hz (le_of_lt xy)
        _ ‚â§ Œ¥ / 2 + Œ¥ / 2 := (add_le_add (le_trans (min_le_left _ _) (min_le_left _ _)) le_rfl)
        _ = Œ¥ := ENNReal.add_halves Œ¥
    show z ‚àà f n
    exact hr (calc
      edist z y ‚â§ min (min (Œ¥ / 2) r) (B (n + 1)) := hz
      _ ‚â§ r := le_trans (min_le_left _ _) (min_le_right _ _))
  choose! center radius Hpos HB Hball using this
  refine fun x ‚Ü¶ (mem_closure_iff_nhds_basis nhds_basis_closed_eball).2 fun Œµ Œµpos ‚Ü¶ ?_
  /- `Œµ` is positive. We have to find a point in the ball of radius `Œµ` around `x` belonging to all
    `f n`. For this, we construct inductively a sequence `F n = (c n, r n)` such that the closed
    ball `closedBall (c n) (r n)` is included in the previous ball and in `f n`, and such that
    `r n` is small enough to ensure that `c n` is a Cauchy sequence. Then `c n` converges to a
    limit which belongs to all the `f n`. -/
  let F : ‚Ñï ‚Üí X √ó ‚Ñù‚â•0‚àû := fun n ‚Ü¶
    Nat.recOn n (Prod.mk x (min Œµ (B 0))) fun n p ‚Ü¶ Prod.mk (center n p.1 p.2) (radius n p.1 p.2)
  let c : ‚Ñï ‚Üí X := fun n ‚Ü¶ (F n).1
  let r : ‚Ñï ‚Üí ‚Ñù‚â•0‚àû := fun n ‚Ü¶ (F n).2
  have rpos : ‚àÄ n, 0 < r n := by
    intro n
    induction n with
    | zero => exact lt_min Œµpos (Bpos 0)
    | succ n hn => exact Hpos n (c n) (r n) hn.ne'
  have r0 : ‚àÄ n, r n ‚â† 0 := fun n ‚Ü¶ (rpos n).ne'
  have rB : ‚àÄ n, r n ‚â§ B n := by
    intro n
    cases n with
    | zero => exact min_le_right _ _
    | succ n => exact HB n (c n) (r n) (r0 n)
  have incl : ‚àÄ n, closedBall (c (n + 1)) (r (n + 1)) ‚äÜ closedBall (c n) (r n) ‚à© f n :=
    fun n ‚Ü¶ Hball n (c n) (r n) (r0 n)
  have cdist : ‚àÄ n, edist (c n) (c (n + 1)) ‚â§ B n := by
    intro n
    rw [edist_comm]
    have A : c (n + 1) ‚àà closedBall (c (n + 1)) (r (n + 1)) := mem_closedBall_self
    have I :=
      calc
        closedBall (c (n + 1)) (r (n + 1)) ‚äÜ closedBall (c n) (r n) :=
          Subset.trans (incl n) inter_subset_left
        _ ‚äÜ closedBall (c n) (B n) := closedBall_subset_closedBall (rB n)
    exact I A
  have : CauchySeq c := cauchySeq_of_edist_le_geometric_two _ ENNReal.one_ne_top cdist
  -- as the sequence `c n` is Cauchy in a complete space, it converges to a limit `y`.
  rcases cauchySeq_tendsto_of_complete this with ‚ü®y, ylim‚ü©
  -- this point `y` will be the desired point. We will check that it belongs to all
  -- `f n` and to `ball x Œµ`.
  use y
  simp only [Set.mem_iInter]
  have I : ‚àÄ n, ‚àÄ m ‚â• n, closedBall (c m) (r m) ‚äÜ closedBall (c n) (r n) := by
    intro n
    refine Nat.le_induction ?_ fun m _ h ‚Ü¶ ?_
    ¬∑ exact Subset.refl _
    ¬∑ exact Subset.trans (incl m) (Subset.trans inter_subset_left h)
  have yball : ‚àÄ n, y ‚àà closedBall (c n) (r n) := by
    intro n
    refine isClosed_closedBall.mem_of_tendsto ylim ?_
    refine (Filter.eventually_ge_atTop n).mono fun m hm ‚Ü¶ ?_
    exact I n m hm mem_closedBall_self
  constructor
  ¬∑ show ‚àÄ n, y ‚àà f n
    intro n
    have : closedBall (c (n + 1)) (r (n + 1)) ‚äÜ f n :=
      Subset.trans (incl n) inter_subset_right
    exact this (yball (n + 1))
  change edist y x ‚â§ Œµ
  exact le_trans (yball 0) (min_le_left _ _)
