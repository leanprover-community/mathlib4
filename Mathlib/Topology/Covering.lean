/-
Copyright (c) 2022 Thomas Browning. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Thomas Browning
-/
import Mathlib.Topology.IsLocalHomeomorph
import Mathlib.Topology.FiberBundle.Basic

/-!
# Covering Maps

This file defines covering maps.

## Main definitions

* `IsEvenlyCovered f x I`: A point `x` is evenly covered by `f : E ‚Üí X` with fiber `I` if `I` is
  discrete and there is a homeomorphism `f ‚Åª¬π' U ‚âÉ‚Çú U √ó I` for some open set `U` containing `x`
  with `f ‚Åª¬π' U` open, such that the induced map `f ‚Åª¬π' U ‚Üí U` coincides with `f`.
* `IsCoveringMap f`: A function `f : E ‚Üí X` is a covering map if every point `x` is evenly
  covered by `f` with fiber `f ‚Åª¬π' {x}`. The fibers `f ‚Åª¬π' {x}` must be discrete, but if `X` is
  not connected, then the fibers `f ‚Åª¬π' {x}` are not necessarily isomorphic. Also, `f` is not
  assumed to be surjective, so the fibers are even allowed to be empty.
-/

open Bundle Topology

variable {E X : Type*} [TopologicalSpace E] [TopologicalSpace X] (f : E ‚Üí X) (s : Set X)

/-- A point `x : X` is evenly covered by `f : E ‚Üí X` if `x` has an evenly covered neighborhood.

**Remark**: `DiscreteTopology I ‚àß ‚àÉ Trivialization I f, x ‚àà t.baseSet` would be a simpler
definition, but unfortunately it does not work if `E` is nonempty but nonetheless `f` has empty
fibers over `s`. If `OpenPartialHomeomorph` could be refactored to work with an empty space and a
nonempty space while preserving the APIs, we could switch back to the definition. -/
def IsEvenlyCovered (x : X) (I : Type*) [TopologicalSpace I] :=
  DiscreteTopology I ‚àß ‚àÉ U : Set X, x ‚àà U ‚àß IsOpen U ‚àß IsOpen (f ‚Åª¬π' U) ‚àß
    ‚àÉ H : f ‚Åª¬π' U ‚âÉ‚Çú U √ó I, ‚àÄ x, (H x).1.1 = f x

namespace IsEvenlyCovered

variable {f} {I : Type*} [TopologicalSpace I]

/-- If `x : X` is evenly covered by `f` with fiber `I`, then `I` is homeomorphic to `f ‚Åª¬π' {x}`. -/
noncomputable def fiberHomeomorph {x : X} (h : IsEvenlyCovered f x I) : I ‚âÉ‚Çú f ‚Åª¬π' {x} := by
  choose _ U hxU hU hfU H hH using h
  exact
  { toFun i := ‚ü®H.symm (‚ü®x, hxU‚ü©, i), by simp [‚Üê hH]‚ü©
    invFun e := (H ‚ü®e, by rwa [Set.mem_preimage, (e.2 : f e = x)]‚ü©).2
    left_inv _ := by simp
    right_inv e := Set.inclusion_injective (Set.preimage_mono (Set.singleton_subset_iff.mpr hxU)) <|
      H.injective <| Prod.ext (Subtype.ext <| by simpa [hH] using e.2.symm) (by simp)
    continuous_toFun := by fun_prop
    continuous_invFun := by fun_prop }

theorem discreteTopology_fiber {x : X} (h : IsEvenlyCovered f x I) : DiscreteTopology (f ‚Åª¬π' {x}) :=
  have := h.1; h.fiberHomeomorph.discreteTopology

/-- If `x` is evenly covered by `f` with nonempty fiber `I`, then we can construct a
trivialization of `f` at `x` with fiber `I`. -/
noncomputable def toTrivialization' {x : X} [Nonempty I] (h : IsEvenlyCovered f x I) :
    Trivialization I f := by
  choose _ U hxU hU hfU H hH using h
  classical exact
  { toFun e := if he : f e ‚àà U then ‚ü®(H ‚ü®e, he‚ü©).1, (H ‚ü®e, he‚ü©).2‚ü© else ‚ü®x, Classical.arbitrary I‚ü©
    invFun xi := H.symm (if hx : xi.1 ‚àà U then ‚ü®xi.1, hx‚ü© else ‚ü®x, hxU‚ü©, xi.2)
    source := f ‚Åª¬π' U
    target := U √óÀ¢ Set.univ
    map_source' e (he : f e ‚àà U) := by simp [he]
    map_target' _ _ := Subtype.coe_prop _
    left_inv' e (he : f e ‚àà U) := by simp [he]
    right_inv' xi := by rintro ‚ü®hx, -‚ü©; simpa [hx] using fun h ‚Ü¶ (h (H.symm _).2).elim
    open_source := hfU
    open_target := hU.prod isOpen_univ
    continuousOn_toFun := continuousOn_iff_continuous_restrict.mpr <|
      ((continuous_subtype_val.prodMap continuous_id).comp H.continuous).congr
      fun ‚ü®e, (he : f e ‚àà U)‚ü© ‚Ü¶ by simp [Prod.map, he]
    continuousOn_invFun := continuousOn_iff_continuous_restrict.mpr <|
      ((continuous_subtype_val.comp H.symm.continuous).comp (by fun_prop :
        Continuous fun ui ‚Ü¶ ‚ü®‚ü®_, ui.2.1‚ü©, ui.1.2‚ü©)).congr fun ‚ü®‚ü®x, i‚ü©, ‚ü®hx, _‚ü©‚ü© ‚Ü¶ by simp [hx]
    baseSet := U
    open_baseSet := hU
    source_eq := rfl
    target_eq := rfl
    proj_toFun e (he : f e ‚àà U) := by simp [he, hH] }

/-- If `x` is evenly covered by `f`, then we can construct a trivialization of `f` at `x`. -/
noncomputable def toTrivialization {x : X} [Nonempty I] (h : IsEvenlyCovered f x I) :
    Trivialization (f ‚Åª¬π' {x}) f :=
  h.toTrivialization'.transFiberHomeomorph h.fiberHomeomorph

theorem mem_toTrivialization_baseSet {x : X} [Nonempty I] (h : IsEvenlyCovered f x I) :
    x ‚àà h.toTrivialization.baseSet := h.2.choose_spec.1

theorem toTrivialization_apply {x : E} [Nonempty I] (h : IsEvenlyCovered f (f x) I) :
    (h.toTrivialization x).2 = ‚ü®x, rfl‚ü© :=
  h.fiberHomeomorph.symm.injective <| by
    simp [toTrivialization, toTrivialization', dif_pos h.2.choose_spec.1, fiberHomeomorph]

protected theorem continuousAt {x : E} (h : IsEvenlyCovered f (f x) I) : ContinuousAt f x :=
  have ‚ü®_, _, hxU, _, _, H, _‚ü© := h
  have : Nonempty I := ‚ü®(H ‚ü®x, hxU‚ü©).2‚ü©
  let e := h.toTrivialization
  e.continuousAt_proj (e.mem_source.mpr (mem_toTrivialization_baseSet h))

theorem of_fiber_homeomorph {J} [TopologicalSpace J] (g : I ‚âÉ‚Çú J) {x : X}
    (h : IsEvenlyCovered f x I) : IsEvenlyCovered f x J :=
  have ‚ü®inst, U, hxU, hU, hfU, H, hH‚ü© := h
  ‚ü®g.discreteTopology, U, hxU, hU, hfU, H.trans (.prodCongr (.refl U) g), fun _ ‚Ü¶ by simp [hH]‚ü©

theorem to_isEvenlyCovered_preimage {x : X} (h : IsEvenlyCovered f x I) :
    IsEvenlyCovered f x (f ‚Åª¬π' {x}) :=
  h.of_fiber_homeomorph h.fiberHomeomorph

theorem of_trivialization [DiscreteTopology I] {x : X} {t : Trivialization I f}
    (hx : x ‚àà t.baseSet) : IsEvenlyCovered f x I :=
  ‚ü®‚Äπ_‚Ä∫, _, hx, t.open_baseSet, t.source_eq ‚ñ∏ t.open_source,
  { toFun e := ‚ü®‚ü®f e, e.2‚ü©, (t e).2‚ü©
    invFun xi := ‚ü®t.invFun (xi.1, xi.2), by
      rw [Set.mem_preimage, ‚Üê t.mem_source]; exact t.map_target (t.target_eq ‚ñ∏ ‚ü®xi.1.2, ‚ü®‚ü©‚ü©)‚ü©
    left_inv e := Subtype.ext <| t.symm_apply_mk_proj (t.mem_source.mpr e.2)
    right_inv xi := by simp [t.proj_symm_apply', t.apply_symm_apply']
    continuous_toFun := (Topology.IsInducing.subtypeVal.prodMap .id).continuous_iff.mpr <|
      (continuousOn_iff_continuous_restrict.mp <| t.continuousOn_toFun.mono t.source_eq.ge).congr
      fun e ‚Ü¶ by simp [t.mk_proj_snd' e.2]
    continuous_invFun := Topology.IsInducing.subtypeVal.continuous_iff.mpr <|
      t.continuousOn_invFun.comp_continuous (continuous_subtype_val.prodMap continuous_id)
      fun ‚ü®x, _‚ü© ‚Ü¶ t.target_eq ‚ñ∏ ‚ü®x.2, ‚ü®‚ü©‚ü© }, fun _ ‚Ü¶ by simp‚ü©

variable (I) in
theorem of_preimage_eq_empty [IsEmpty I] {x : X} {U : Set X} (hUx : U ‚àà ùìù x) (hfU : f ‚Åª¬π' U = ‚àÖ) :
    IsEvenlyCovered f x I :=
  have ‚ü®V, hVU, hV, hxV‚ü© := mem_nhds_iff.mp hUx
  have hfV : f‚Åª¬π' V = ‚àÖ := Set.eq_empty_of_subset_empty ((Set.preimage_mono hVU).trans hfU.le)
  have := Set.isEmpty_coe_sort.mpr hfV
  ‚ü®inferInstance, _, hxV, hV, hfV ‚ñ∏ isOpen_empty, .empty, isEmptyElim‚ü©

end IsEvenlyCovered

/-- A covering map is a continuous function `f : E ‚Üí X` with discrete fibers such that each point
  of `X` has an evenly covered neighborhood. -/
def IsCoveringMapOn :=
  ‚àÄ x ‚àà s, IsEvenlyCovered f x (f ‚Åª¬π' {x})

namespace IsCoveringMapOn

theorem of_isEmpty [IsEmpty E] : IsCoveringMapOn f s := fun _ _ ‚Ü¶ .to_isEvenlyCovered_preimage
  (.of_preimage_eq_empty Empty Filter.univ_mem <| Set.eq_empty_of_isEmpty _)

/-- A constructor for `IsCoveringMapOn` when there are both empty and nonempty fibers. -/
theorem mk' (F : s ‚Üí Type*) [‚àÄ x : s, TopologicalSpace (F x)] [hF : ‚àÄ x : s, DiscreteTopology (F x)]
    (t : ‚àÄ x : s, x.1 ‚àà Set.range f ‚Üí {t : Trivialization (F x) f // x.1 ‚àà t.baseSet})
    (h : ‚àÄ x : s, x.1 ‚àâ Set.range f ‚Üí ‚àÉ U ‚àà ùìù x.1, f ‚Åª¬π' U = ‚àÖ) :
    IsCoveringMapOn f s := fun x hx ‚Ü¶ by
  lift x to s using hx
  by_cases hxf : x.1 ‚àà Set.range f
  ¬∑ exact .to_isEvenlyCovered_preimage (.of_trivialization (t x hxf).2)
  ¬∑ have ‚ü®U, hUx, hfU‚ü© := h x hxf
    exact .to_isEvenlyCovered_preimage (.of_preimage_eq_empty Empty hUx hfU)

theorem mk (F : s ‚Üí Type*) [‚àÄ x, TopologicalSpace (F x)] [hF : ‚àÄ x, DiscreteTopology (F x)]
    (e : ‚àÄ x, Trivialization (F x) f) (h : ‚àÄ x, x.1 ‚àà (e x).baseSet) :
    IsCoveringMapOn f s := by
  cases isEmpty_or_nonempty E
  ¬∑ exact .of_isEmpty _ _
  refine .mk' _ _ _ (fun x _ ‚Ü¶ ‚ü®e x, h x‚ü©) fun x hx ‚Ü¶ (hx ?_).elim
  exact ‚ü®(e x).invFun (x, (e x <| Classical.arbitrary E).2), (e x).proj_symm_apply' (h x)‚ü©

variable {f s}

protected theorem continuousAt (hf : IsCoveringMapOn f s) {x : E} (hx : f x ‚àà s) :
    ContinuousAt f x := (hf (f x) hx).continuousAt

protected theorem continuousOn (hf : IsCoveringMapOn f s) : ContinuousOn f (f ‚Åª¬π' s) :=
  continuousOn_of_forall_continuousAt fun _ ‚Ü¶ hf.continuousAt

protected theorem isLocalHomeomorphOn (hf : IsCoveringMapOn f s) :
    IsLocalHomeomorphOn f (f ‚Åª¬π' s) := by
  refine IsLocalHomeomorphOn.mk f (f ‚Åª¬π' s) fun x hx ‚Ü¶ ?_
  have : Nonempty (f ‚Åª¬π' {f x}) := ‚ü®‚ü®x, rfl‚ü©‚ü©
  let e := (hf (f x) hx).toTrivialization
  have h := (hf (f x) hx).mem_toTrivialization_baseSet
  let he := e.mem_source.2 h
  refine
    ‚ü®e.toOpenPartialHomeomorph.trans
        { toFun := fun p => p.1
          invFun := fun p => ‚ü®p, x, rfl‚ü©
          source := e.baseSet √óÀ¢ ({‚ü®x, rfl‚ü©} : Set (f ‚Åª¬π' {f x}))
          target := e.baseSet
          open_source :=
            e.open_baseSet.prod (discreteTopology_iff_isOpen_singleton.1 (hf (f x) hx).1 ‚ü®x, rfl‚ü©)
          open_target := e.open_baseSet
          map_source' := fun p => And.left
          map_target' := fun p hp => ‚ü®hp, rfl‚ü©
          left_inv' := fun p hp => Prod.ext rfl hp.2.symm
          right_inv' := fun p _ => rfl
          continuousOn_toFun := continuousOn_fst
          continuousOn_invFun := by fun_prop },
      ‚ü®he, by rwa [e.toOpenPartialHomeomorph.symm_symm, e.proj_toFun x he],
        (hf (f x) hx).toTrivialization_apply‚ü©,
      fun p h => (e.proj_toFun p h.1).symm‚ü©

end IsCoveringMapOn

/-- A covering map is a continuous function `f : E ‚Üí X` with discrete fibers such that each point
  of `X` has an evenly covered neighborhood. -/
def IsCoveringMap :=
  ‚àÄ x, IsEvenlyCovered f x (f ‚Åª¬π' {x})

variable {f}

theorem isCoveringMap_iff_isCoveringMapOn_univ : IsCoveringMap f ‚Üî IsCoveringMapOn f Set.univ := by
  simp only [IsCoveringMap, IsCoveringMapOn, Set.mem_univ, forall_true_left]

protected theorem IsCoveringMap.isCoveringMapOn (hf : IsCoveringMap f) :
    IsCoveringMapOn f Set.univ :=
  isCoveringMap_iff_isCoveringMapOn_univ.mp hf

variable (f)

namespace IsCoveringMap

theorem of_isEmpty [IsEmpty E] : IsCoveringMap f :=
  isCoveringMap_iff_isCoveringMapOn_univ.mpr <| .of_isEmpty _ _

theorem of_discreteTopology [DiscreteTopology E] [DiscreteTopology X] : IsCoveringMap f :=
  fun x ‚Ü¶ ‚ü®inferInstance, {x}, rfl, isOpen_discrete _, isOpen_discrete _,
    { toFun e := ‚ü®‚ü®x, rfl‚ü©, e‚ü©
      invFun xi := xi.2
      left_inv _ := rfl
      right_inv _ := Prod.ext (Subsingleton.elim ..) rfl },
    (¬∑.2.symm)‚ü©

/-- A constructor for `IsCoveringMap` when there are both empty and nonempty fibers. -/
theorem mk' (F : X ‚Üí Type*) [‚àÄ x, TopologicalSpace (F x)] [‚àÄ x, DiscreteTopology (F x)]
    (t : ‚àÄ x, x ‚àà Set.range f ‚Üí {t : Trivialization (F x) f // x ‚àà t.baseSet})
    (h : IsClosed (Set.range f)) : IsCoveringMap f :=
  isCoveringMap_iff_isCoveringMapOn_univ.mpr <| .mk' f _ _ (fun x h ‚Ü¶ t x h) fun _x hx ‚Ü¶
    ‚ü®_, h.isOpen_compl.mem_nhds hx, Set.eq_empty_of_forall_notMem fun x h ‚Ü¶ h ‚ü®x, rfl‚ü©‚ü©

theorem mk (F : X ‚Üí Type*) [‚àÄ x, TopologicalSpace (F x)] [‚àÄ x, DiscreteTopology (F x)]
    (e : ‚àÄ x, Trivialization (F x) f) (h : ‚àÄ x, x ‚àà (e x).baseSet) : IsCoveringMap f :=
  isCoveringMap_iff_isCoveringMapOn_univ.mpr <| .mk _ _ _ _ fun x ‚Ü¶ h x

variable {f}
variable (hf : IsCoveringMap f)
include hf

protected theorem continuous : Continuous f :=
  continuousOn_univ.mp hf.isCoveringMapOn.continuousOn

protected theorem isLocalHomeomorph : IsLocalHomeomorph f :=
  isLocalHomeomorph_iff_isLocalHomeomorphOn_univ.mpr hf.isCoveringMapOn.isLocalHomeomorphOn

protected theorem isOpenMap : IsOpenMap f :=
  hf.isLocalHomeomorph.isOpenMap

theorem isQuotientMap (hf' : Function.Surjective f) : IsQuotientMap f :=
  hf.isOpenMap.isQuotientMap hf.continuous hf'

protected theorem isSeparatedMap : IsSeparatedMap f :=
  fun e‚ÇÅ e‚ÇÇ he hne ‚Ü¶ by
    have : Nonempty (f ‚Åª¬π' {f e‚ÇÅ}) := ‚ü®‚ü®e‚ÇÅ, rfl‚ü©‚ü©
    specialize hf (f e‚ÇÅ)
    let t := hf.toTrivialization
    have := hf.discreteTopology_fiber
    have he‚ÇÅ := hf.mem_toTrivialization_baseSet
    have he‚ÇÇ := he‚ÇÅ; simp_rw [he] at he‚ÇÇ; rw [‚Üê t.mem_source] at he‚ÇÅ he‚ÇÇ
    refine ‚ü®t.source ‚à© (Prod.snd ‚àò t) ‚Åª¬π' {(t e‚ÇÅ).2}, t.source ‚à© (Prod.snd ‚àò t) ‚Åª¬π' {(t e‚ÇÇ).2},
      ?_, ?_, ‚ü®he‚ÇÅ, rfl‚ü©, ‚ü®he‚ÇÇ, rfl‚ü©, Set.disjoint_left.mpr fun x h‚ÇÅ h‚ÇÇ ‚Ü¶ hne (t.injOn he‚ÇÅ he‚ÇÇ ?_)‚ü©
    iterate 2
      exact t.continuousOn_toFun.isOpen_inter_preimage t.open_source
        (continuous_snd.isOpen_preimage _ <| isOpen_discrete _)
    refine Prod.ext ?_ (h‚ÇÅ.2.symm.trans h‚ÇÇ.2)
    rwa [t.proj_toFun e‚ÇÅ he‚ÇÅ, t.proj_toFun e‚ÇÇ he‚ÇÇ]

variable {A} [TopologicalSpace A] {s : Set A} {g g‚ÇÅ g‚ÇÇ : A ‚Üí E}

/-- Proposition 1.34 of [hatcher02]. -/
theorem eq_of_comp_eq [PreconnectedSpace A] (h‚ÇÅ : Continuous g‚ÇÅ) (h‚ÇÇ : Continuous g‚ÇÇ)
    (he : f ‚àò g‚ÇÅ = f ‚àò g‚ÇÇ) (a : A) (ha : g‚ÇÅ a = g‚ÇÇ a) : g‚ÇÅ = g‚ÇÇ :=
  hf.isSeparatedMap.eq_of_comp_eq hf.isLocalHomeomorph.isLocallyInjective h‚ÇÅ h‚ÇÇ he a ha

theorem const_of_comp [PreconnectedSpace A] (cont : Continuous g)
    (he : ‚àÄ a a', f (g a) = f (g a')) (a a') : g a = g a' :=
  hf.isSeparatedMap.const_of_comp hf.isLocalHomeomorph.isLocallyInjective cont he a a'

theorem eqOn_of_comp_eqOn (hs : IsPreconnected s) (h‚ÇÅ : ContinuousOn g‚ÇÅ s) (h‚ÇÇ : ContinuousOn g‚ÇÇ s)
    (he : s.EqOn (f ‚àò g‚ÇÅ) (f ‚àò g‚ÇÇ)) {a : A} (has : a ‚àà s) (ha : g‚ÇÅ a = g‚ÇÇ a) : s.EqOn g‚ÇÅ g‚ÇÇ :=
  hf.isSeparatedMap.eqOn_of_comp_eqOn hf.isLocalHomeomorph.isLocallyInjective hs h‚ÇÅ h‚ÇÇ he has ha

theorem constOn_of_comp (hs : IsPreconnected s) (cont : ContinuousOn g s)
    (he : ‚àÄ a ‚àà s, ‚àÄ a' ‚àà s, f (g a) = f (g a'))
    {a a'} (ha : a ‚àà s) (ha' : a' ‚àà s) : g a = g a' :=
  hf.isSeparatedMap.constOn_of_comp hf.isLocalHomeomorph.isLocallyInjective hs cont he ha ha'

end IsCoveringMap

variable {f}

protected theorem IsFiberBundle.isCoveringMap {F : Type*} [TopologicalSpace F] [DiscreteTopology F]
    (hf : ‚àÄ x : X, ‚àÉ e : Trivialization F f, x ‚àà e.baseSet) : IsCoveringMap f :=
  IsCoveringMap.mk f (fun _ => F) (fun x => Classical.choose (hf x)) fun x =>
    Classical.choose_spec (hf x)

protected theorem FiberBundle.isCoveringMap {F : Type*} {E : X ‚Üí Type*} [TopologicalSpace F]
    [DiscreteTopology F] [TopologicalSpace (Bundle.TotalSpace F E)] [‚àÄ x, TopologicalSpace (E x)]
    [FiberBundle F E] : IsCoveringMap (œÄ F E) :=
  IsFiberBundle.isCoveringMap fun x => ‚ü®trivializationAt F E x, mem_baseSet_trivializationAt F E x‚ü©
