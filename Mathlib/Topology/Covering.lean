/-
Copyright (c) 2022 Thomas Browning. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Thomas Browning
-/
import Mathlib.Topology.Instances.AddCircle
import Mathlib.Topology.IsLocallyHomeomorph
import Mathlib.Topology.FiberBundle.Basic

#align_import topology.covering from "leanprover-community/mathlib"@"e473c3198bb41f68560cab68a0529c854b618833"

/-!
# Covering Maps

This file defines covering maps.

## Main definitions

* `IsEvenlyCovered f x I`: A point `x` is evenly covered by `f : E ‚Üí X` with fiber `I` if `I` is
  discrete and there is a `Trivialization` of `f` at `x` with fiber `I`.
* `IsCoveringMap f`: A function `f : E ‚Üí X` is a covering map if every point `x` is evenly
  covered by `f` with fiber `f ‚Åª¬π' {x}`. The fibers `f ‚Åª¬π' {x}` must be discrete, but if `X` is
  not connected, then the fibers `f ‚Åª¬π' {x}` are not necessarily isomorphic. Also, `f` is not
  assumed to be surjective, so the fibers are even allowed to be empty.
-/


open Bundle

variable {E X : Type*} [TopologicalSpace E] [TopologicalSpace X] (f : E ‚Üí X) (s : Set X)

/-- A point `x : X` is evenly covered by `f : E ‚Üí X` if `x` has an evenly covered neighborhood. -/
def IsEvenlyCovered (x : X) (I : Type*) [TopologicalSpace I] :=
  DiscreteTopology I ‚àß ‚àÉ t : Trivialization I f, x ‚àà t.baseSet
#align is_evenly_covered IsEvenlyCovered

namespace IsEvenlyCovered

variable {f}

/-- If `x` is evenly covered by `f`, then we can construct a trivialization of `f` at `x`. -/
noncomputable def toTrivialization {x : X} {I : Type*} [TopologicalSpace I]
    (h : IsEvenlyCovered f x I) : Trivialization (f ‚Åª¬π' {x}) f :=
  (Classical.choose h.2).transFiberHomeomorph
    ((Classical.choose h.2).preimageSingletonHomeomorph (Classical.choose_spec h.2)).symm
#align is_evenly_covered.to_trivialization IsEvenlyCovered.toTrivialization

theorem mem_toTrivialization_baseSet {x : X} {I : Type*} [TopologicalSpace I]
    (h : IsEvenlyCovered f x I) : x ‚àà h.toTrivialization.baseSet :=
  Classical.choose_spec h.2
#align is_evenly_covered.mem_to_trivialization_base_set IsEvenlyCovered.mem_toTrivialization_baseSet

theorem toTrivialization_apply {x : E} {I : Type*} [TopologicalSpace I]
    (h : IsEvenlyCovered f (f x) I) : (h.toTrivialization x).2 = ‚ü®x, rfl‚ü© :=
  let e := Classical.choose h.2
  let h := Classical.choose_spec h.2
  let he := e.mk_proj_snd' h
  Subtype.ext
    ((e.toLocalEquiv.eq_symm_apply (e.mem_source.mpr h)
            (by rwa [he, e.mem_target, e.coe_fst (e.mem_source.mpr h)])).mpr
        he.symm).symm
#align is_evenly_covered.to_trivialization_apply IsEvenlyCovered.toTrivialization_apply

protected theorem continuousAt {x : E} {I : Type*} [TopologicalSpace I]
    (h : IsEvenlyCovered f (f x) I) : ContinuousAt f x :=
  let e := h.toTrivialization
  e.continuousAt_proj (e.mem_source.mpr (mem_toTrivialization_baseSet h))
#align is_evenly_covered.continuous_at IsEvenlyCovered.continuousAt

theorem to_isEvenlyCovered_preimage {x : X} {I : Type*} [TopologicalSpace I]
    (h : IsEvenlyCovered f x I) : IsEvenlyCovered f x (f ‚Åª¬π' {x}) :=
  let ‚ü®_, h2‚ü© := h
  ‚ü®((Classical.choose h2).preimageSingletonHomeomorph
          (Classical.choose_spec h2)).embedding.discreteTopology,
    _, h.mem_toTrivialization_baseSet‚ü©
#align is_evenly_covered.to_is_evenly_covered_preimage IsEvenlyCovered.to_isEvenlyCovered_preimage

end IsEvenlyCovered

/-- A covering map is a continuous function `f : E ‚Üí X` with discrete fibers such that each point
  of `X` has an evenly covered neighborhood. -/
def IsCoveringMapOn :=
  ‚àÄ x ‚àà s, IsEvenlyCovered f x (f ‚Åª¬π' {x})
#align is_covering_map_on IsCoveringMapOn

namespace IsCoveringMapOn

theorem mk (F : X ‚Üí Type*) [‚àÄ x, TopologicalSpace (F x)] [hF : ‚àÄ x, DiscreteTopology (F x)]
    (e : ‚àÄ x ‚àà s, Trivialization (F x) f) (h : ‚àÄ (x : X) (hx : x ‚àà s), x ‚àà (e x hx).baseSet) :
    IsCoveringMapOn f s := fun x hx =>
  IsEvenlyCovered.to_isEvenlyCovered_preimage ‚ü®hF x, e x hx, h x hx‚ü©
#align is_covering_map_on.mk IsCoveringMapOn.mk

variable {f} {s}

protected theorem continuousAt (hf : IsCoveringMapOn f s) {x : E} (hx : f x ‚àà s) :
    ContinuousAt f x :=
  (hf (f x) hx).continuousAt
#align is_covering_map_on.continuous_at IsCoveringMapOn.continuousAt

protected theorem continuousOn (hf : IsCoveringMapOn f s) : ContinuousOn f (f ‚Åª¬π' s) :=
  ContinuousAt.continuousOn fun _ => hf.continuousAt
#align is_covering_map_on.continuous_on IsCoveringMapOn.continuousOn

protected theorem isLocallyHomeomorphOn (hf : IsCoveringMapOn f s) :
    IsLocallyHomeomorphOn f (f ‚Åª¬π' s) := by
  refine' IsLocallyHomeomorphOn.mk f (f ‚Åª¬π' s) fun x hx => _
  let e := (hf (f x) hx).toTrivialization
  have h := (hf (f x) hx).mem_toTrivialization_baseSet
  let he := e.mem_source.2 h
  refine'
    ‚ü®e.toLocalHomeomorph.trans
        { toFun := fun p => p.1
          invFun := fun p => ‚ü®p, x, rfl‚ü©
          source := e.baseSet √óÀ¢ ({‚ü®x, rfl‚ü©} : Set (f ‚Åª¬π' {f x}))
          target := e.baseSet
          open_source :=
            e.open_baseSet.prod (singletons_open_iff_discrete.2 (hf (f x) hx).1 ‚ü®x, rfl‚ü©)
          open_target := e.open_baseSet
          map_source' := fun p => And.left
          map_target' := fun p hp => ‚ü®hp, rfl‚ü©
          left_inv' := fun p hp => Prod.ext rfl hp.2.symm
          right_inv' := fun p _ => rfl
          continuous_toFun := continuous_fst.continuousOn
          continuous_invFun := (continuous_id'.prod_mk continuous_const).continuousOn },
      ‚ü®he, by rwa [e.toLocalHomeomorph.symm_symm, e.proj_toFun x he],
        (hf (f x) hx).toTrivialization_apply‚ü©,
      fun p h => (e.proj_toFun p h.1).symm‚ü©
#align is_covering_map_on.is_locally_homeomorph_on IsCoveringMapOn.isLocallyHomeomorphOn

end IsCoveringMapOn

/-- A covering map is a continuous function `f : E ‚Üí X` with discrete fibers such that each point
  of `X` has an evenly covered neighborhood. -/
def IsCoveringMap :=
  ‚àÄ x, IsEvenlyCovered f x (f ‚Åª¬π' {x})
#align is_covering_map IsCoveringMap

variable {f}

theorem isCoveringMap_iff_isCoveringMapOn_univ : IsCoveringMap f ‚Üî IsCoveringMapOn f Set.univ := by
  simp only [IsCoveringMap, IsCoveringMapOn, Set.mem_univ, forall_true_left]
#align is_covering_map_iff_is_covering_map_on_univ isCoveringMap_iff_isCoveringMapOn_univ

protected theorem IsCoveringMap.isCoveringMapOn (hf : IsCoveringMap f) :
    IsCoveringMapOn f Set.univ :=
  isCoveringMap_iff_isCoveringMapOn_univ.mp hf
#align is_covering_map.is_covering_map_on IsCoveringMap.isCoveringMapOn

variable (f)

namespace IsCoveringMap

theorem mk (F : X ‚Üí Type*) [‚àÄ x, TopologicalSpace (F x)] [‚àÄ x, DiscreteTopology (F x)]
    (e : ‚àÄ x, Trivialization (F x) f) (h : ‚àÄ x, x ‚àà (e x).baseSet) : IsCoveringMap f :=
  isCoveringMap_iff_isCoveringMapOn_univ.mpr
    (IsCoveringMapOn.mk f Set.univ F (fun x _ => e x) fun x _ => h x)
#align is_covering_map.mk IsCoveringMap.mk

variable {f}

protected theorem continuous (hf : IsCoveringMap f) : Continuous f :=
  continuous_iff_continuousOn_univ.mpr hf.isCoveringMapOn.continuousOn
#align is_covering_map.continuous IsCoveringMap.continuous

protected theorem isLocallyHomeomorph (hf : IsCoveringMap f) : IsLocallyHomeomorph f :=
  isLocallyHomeomorph_iff_isLocallyHomeomorphOn_univ.mpr hf.isCoveringMapOn.isLocallyHomeomorphOn
#align is_covering_map.is_locally_homeomorph IsCoveringMap.isLocallyHomeomorph

protected theorem isOpenMap (hf : IsCoveringMap f) : IsOpenMap f :=
  hf.isLocallyHomeomorph.isOpenMap
#align is_covering_map.is_open_map IsCoveringMap.isOpenMap

protected theorem quotientMap (hf : IsCoveringMap f) (hf' : Function.Surjective f) :
    QuotientMap f :=
  hf.isOpenMap.to_quotientMap hf.continuous hf'
#align is_covering_map.quotient_map IsCoveringMap.quotientMap

end IsCoveringMap

variable {f}

protected theorem IsFiberBundle.isCoveringMap {F : Type*} [TopologicalSpace F] [DiscreteTopology F]
    (hf : ‚àÄ x : X, ‚àÉ e : Trivialization F f, x ‚àà e.baseSet) : IsCoveringMap f :=
  IsCoveringMap.mk f (fun _ => F) (fun x => Classical.choose (hf x)) fun x =>
    Classical.choose_spec (hf x)
#align is_fiber_bundle.is_covering_map IsFiberBundle.isCoveringMap

protected theorem FiberBundle.isCoveringMap {F : Type*} {E : X ‚Üí Type*} [TopologicalSpace F]
    [DiscreteTopology F] [TopologicalSpace (Bundle.TotalSpace F E)] [‚àÄ x, TopologicalSpace (E x)]
    [FiberBundle F E] : IsCoveringMap (œÄ F E) :=
  IsFiberBundle.isCoveringMap fun x => ‚ü®trivializationAt F E x, mem_baseSet_trivializationAt F E x‚ü©
#align fiber_bundle.is_covering_map FiberBundle.isCoveringMap

/-- Let `f : E ‚Üí X` be a (not necessarily continuous) map between topological spaces, and let
  `V` be an open subset of `X`. Suppose that there is a family `U` of disjoint subsets of `E`
  that covers `f‚Åª¬π(V)` such that for every `i`, (1) `f` is injective on `U_i`, (2) `V` is
  contained in the image `f(U_i), and (3) the open sets in `V` are determined by their preimages
  in `U_i`. Then `f` admits a `Trivialization` over the base set `V`. -/
noncomputable def IsOpen.trivialization_discrete (hE : Nonempty E ‚à® f.Surjective)
    {Œπ} [Nonempty Œπ] [t : TopologicalSpace Œπ] [d : DiscreteTopology Œπ] (U : Œπ ‚Üí Set E) (V : Set X)
    (open_V : IsOpen V) (open_iff : ‚àÄ i {W}, W ‚äÜ V ‚Üí (IsOpen W ‚Üî IsOpen (f ‚Åª¬π' W ‚à© U i)))
    (inj : ‚àÄ i, (U i).InjOn f) (surj : ‚àÄ i, (U i).SurjOn f V)
    (disjoint : ‚àÄ {i j}, i ‚â† j ‚Üí Disjoint (U i) (U j)) (exhaustive : f ‚Åª¬π' V ‚äÜ ‚ãÉ i, U i) :
    Trivialization Œπ f := by
  have exhaustive' := exhaustive
  simp_rw [Set.subset_def, Set.mem_iUnion] at exhaustive
  choose idx idx_U using exhaustive
  choose inv inv_U f_inv using surj
  classical
  let F : LocalEquiv E (X √ó Œπ); refine
  { toFun := fun e ‚Ü¶ (f e, if he : f e ‚àà V then idx e he else Classical.arbitrary Œπ),
    invFun := fun x ‚Ü¶ if hx : x.1 ‚àà V then inv x.2 hx else
      if h : Nonempty E then Classical.arbitrary E else (hE.resolve_left h x.1).choose,
    source := f ‚Åª¬π' V,
    target := V √óÀ¢ Set.univ,
    map_source' := fun x hx ‚Ü¶ ‚ü®hx, trivial‚ü©
    map_target' := fun x ‚ü®hx, _‚ü© ‚Ü¶ by dsimp only; rw [dif_pos hx]; apply (f_inv _ hx).symm ‚ñ∏ hx,
    left_inv' := fun e he ‚Ü¶ ?_,
    right_inv' := fun x hx ‚Ü¶ ?_ }
  ¬∑ change f e ‚àà V at he; dsimp only; simp_rw [dif_pos he]
    exact inj _ (inv_U _ he) (idx_U e he) (f_inv _ _)
  ¬∑ dsimp only; rw [dif_pos hx.1]
    refine Prod.ext (f_inv _ hx.1) ?_
    dsimp only; rw [dif_pos ((f_inv _ hx.1).symm ‚ñ∏ hx.1)]
    by_contra h; exact (disjoint h).le_bot ‚ü®idx_U _ _, inv_U _ _‚ü©
  have open_preim : ‚àÄ {W}, W ‚äÜ V ‚Üí IsOpen W ‚Üí IsOpen (f ‚Åª¬π' W)
  ¬∑ intro W hWV hoW
    convert isOpen_iUnion (fun i ‚Ü¶ (open_iff i hWV).mp hoW)
    rw [‚Üê Set.inter_iUnion, eq_comm, Set.inter_eq_left]
    exact (Set.preimage_mono hWV).trans exhaustive'
  have open_source : IsOpen F.source := open_preim subset_rfl open_V
  have cont_f : ContinuousOn f F.source := (continuousOn_open_iff open_source).mpr
    fun W open_W ‚Ü¶ open_preim (V.inter_subset_left W) (open_V.inter open_W)
  refine
  { toLocalEquiv := F,
    open_source := open_source,
    open_target := open_V.prod isOpen_univ,
    continuous_toFun := cont_f.prod <| ContinuousAt.continuousOn fun e he ‚Ü¶
      continuous_const (b := idx e he) |>.continuousAt.congr <| mem_nhds_iff.mpr
        ‚ü®U (idx e he) ‚à© F.source, fun e' he' ‚Ü¶ ?_, ?_, idx_U e he, he‚ü©
    continuous_invFun := continuousOn_prod_of_discrete_right.mpr fun i ‚Ü¶ ?_,
    baseSet := V,
    open_baseSet := open_V,
    source_eq := rfl,
    target_eq := rfl,
    proj_toFun := fun _ _ ‚Ü¶ rfl }
  ¬∑ by_contra h; apply (disjoint h).le_bot
    dsimp only; erw [dif_pos he'.2]; exact ‚ü®he'.1, idx_U _ _‚ü©
  ¬∑ rwa [Set.inter_comm, ‚Üê open_iff _ subset_rfl]
  ¬∑ simp_rw [Set.prod_mk_mem_set_prod_eq, Set.mem_univ, and_true]
    refine (continuousOn_open_iff open_V).mpr fun W open_W ‚Ü¶ ?_
    rw [open_iff i (V.inter_subset_left _)]
    convert ((open_iff i subset_rfl).mp open_V).inter open_W using 1
    ext e; refine and_right_comm.trans (and_congr_right fun ‚ü®hV, hU‚ü© ‚Ü¶ ?_)
    rw [Set.mem_preimage, dif_pos hV, inj i (inv_U i _) hU (f_inv i _)]

namespace QuotientMap

open Topology

variable {G} [t : TopologicalSpace G] [d : DiscreteTopology G]
  [Group G] [MulAction G E] [ContinuousConstSMul G E]
  {p : E ‚Üí X} (hp : QuotientMap p) (hpG : ‚àÄ {e‚ÇÅ e‚ÇÇ}, p e‚ÇÅ = p e‚ÇÇ ‚Üî e‚ÇÅ ‚àà MulAction.orbit G e‚ÇÇ)

/-- If a group `G` acts on a space `E` and `U` is an open subset disjoint from all other
  `G`-translates of itself, and `p` is a quotient map by this action, then `p` admits a
  `Trivialization` over the base set `p(U)`. -/
@[to_additive] noncomputable def trivialization_of_mulAction (U : Set E) (open_U : IsOpen U)
    (disjoint : ‚àÄ g : G, (g ‚Ä¢ ¬∑) '' U ‚à© U ‚â† ‚àÖ ‚Üí g = 1) : Trivialization G p := by
  have pGE : ‚àÄ (g : G) e, p (g ‚Ä¢ e) = p e := fun g e ‚Ü¶ hpG.mpr ‚ü®g, rfl‚ü©
  simp_rw [‚Üê Set.nonempty_iff_ne_empty] at disjoint
  have preim_im : p ‚Åª¬π' (p '' U) = ‚ãÉ g : G, (g ‚Ä¢ ¬∑) ‚Åª¬π' U
  ¬∑ ext e; refine ‚ü®fun ‚ü®e', heU, he‚ü© ‚Ü¶ ?_, ?_‚ü©
    ¬∑ obtain ‚ü®g, rfl‚ü© := hpG.mp he; exact ‚ü®_, ‚ü®g, rfl‚ü©, heU‚ü©
    ¬∑ intro ‚ü®_, ‚ü®g, rfl‚ü©, hg‚ü©; exact ‚ü®_, hg, pGE g e‚ü©
  refine IsOpen.trivialization_discrete (Or.inr hp.surjective) (fun g ‚Ü¶ (g ‚Ä¢ ¬∑) ‚Åª¬π' U) (p '' U)
    ?_ (fun g W hWU ‚Ü¶ ‚ü®fun hoW ‚Ü¶ (hoW.preimage hp.continuous).inter (open_U.preimage <|
      continuous_const_smul g), fun isOpen ‚Ü¶ hp.isOpen_preimage.mp ?_‚ü©) (fun g e‚ÇÅ h‚ÇÅ e‚ÇÇ h‚ÇÇ he ‚Ü¶ ?_)
    ?_ (fun {g‚ÇÅ g‚ÇÇ} hne ‚Ü¶ disjoint_iff_inf_le.mpr fun e ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚Ü¶ hne <|
      mul_inv_eq_one.mp (disjoint _ ‚ü®_, ‚ü®_, h‚ÇÇ, ?_‚ü©, h‚ÇÅ‚ü©)) preim_im.subset
  ¬∑ rw [‚Üê hp.isOpen_preimage, preim_im]
    exact isOpen_iUnion fun g ‚Ü¶ open_U.preimage (continuous_const_smul g)
  ¬∑ convert isOpen_iUnion fun g : G ‚Ü¶ isOpen.preimage (continuous_const_smul g)
    ext e; refine ‚ü®fun hW ‚Ü¶ ?_, ?_‚ü©
    ¬∑ obtain ‚ü®e', he', hpe‚ü© := hWU hW
      obtain ‚ü®g', rfl‚ü© := hpG.mp hpe
      refine ‚ü®_, ‚ü®g‚Åª¬π * g', rfl‚ü©, ?_, ?_‚ü©
      ¬∑ apply Set.mem_of_eq_of_mem (pGE _ e) hW
      ¬∑ apply Set.mem_of_eq_of_mem _ he'; dsimp only; rw [mul_smul, smul_inv_smul]
    ¬∑ rintro ‚ü®_, ‚ü®g, rfl‚ü©, hW, -‚ü©; apply Set.mem_of_eq_of_mem (pGE _ e).symm hW
  ¬∑ rw [‚Üê pGE g, ‚Üê pGE g e‚ÇÇ] at he; obtain ‚ü®g', he‚ü© := hpG.mp he
    rw [‚Üê smul_left_cancel_iff g, ‚Üê he, disjoint g' ‚ü®_, ‚ü®_, h‚ÇÇ, he‚ü©, h‚ÇÅ‚ü©]; apply one_smul
  ¬∑ rintro g x ‚ü®e, hU, rfl‚ü©; refine ‚ü®g‚Åª¬π ‚Ä¢ e, ?_, pGE _ e‚ü©; apply (smul_inv_smul g e).symm ‚ñ∏ hU
  ¬∑ dsimp only; rw [mul_smul, inv_smul_smul]

@[to_additive] lemma isCoveringMapOn_of_mulAction
    (disjoint : ‚àÄ e : E, ‚àÉ U ‚àà ùìù e, ‚àÄ g : G, (g ‚Ä¢ ¬∑) '' U ‚à© U ‚â† ‚àÖ ‚Üí g ‚Ä¢ e = e) :
    IsCoveringMapOn p (p '' {e | MulAction.stabilizer G e = ‚ä•}) := by
  letI : TopologicalSpace G := ‚ä•; have : DiscreteTopology G := ‚ü®rfl‚ü©
  suffices : ‚àÄ x ‚àà p '' {e | MulAction.stabilizer G e = ‚ä•}, ‚àÉ t : Trivialization G p, x ‚àà t.baseSet
  ¬∑ choose t ht using this; exact IsCoveringMapOn.mk _ _ (fun _ ‚Ü¶ G) t ht
  rintro x ‚ü®e, he, rfl‚ü©
  obtain ‚ü®U, heU, hU‚ü© := disjoint e
  refine ‚ü®hp.trivialization_of_mulAction hpG (interior U) isOpen_interior
    fun g hg ‚Ü¶ ?_, e, mem_interior_iff_mem_nhds.mpr heU, rfl‚ü©
  rw [‚Üê Subgroup.mem_bot, ‚Üê he]; apply hU; contrapose! hg; exact Set.subset_eq_empty
    (Set.inter_subset_inter (Set.image_subset _ interior_subset) interior_subset) hg

@[to_additive] lemma isCoveringMap_of_mulAction
    (disjoint : ‚àÄ e : E, ‚àÉ U ‚àà ùìù e, ‚àÄ g : G, (g ‚Ä¢ ¬∑) '' U ‚à© U ‚â† ‚àÖ ‚Üí g = 1) : IsCoveringMap p :=
  isCoveringMap_iff_isCoveringMapOn_univ.mpr <| by
    convert ‚Üê hp.isCoveringMapOn_of_mulAction hpG fun e ‚Ü¶ ?_
    ¬∑ refine Set.eq_univ_of_forall fun x ‚Ü¶ ?_
      obtain ‚ü®e, rfl‚ü© := hp.surjective x
      refine ‚ü®e, (Subgroup.eq_bot_iff_forall _).mpr fun g hg ‚Ü¶ ?_, rfl‚ü©
      obtain ‚ü®U, hU, hGU‚ü© := disjoint e
      have := mem_of_mem_nhds hU
      apply hGU g (Set.nonempty_iff_ne_empty.mp ‚ü®e, ‚ü®e, this, hg‚ü©, this‚ü©)
    ¬∑ obtain ‚ü®U, hU, hGU‚ü© := disjoint e
      refine ‚ü®U, hU, fun g hg ‚Ü¶ ?_‚ü©; rw [hGU g hg, one_smul]

@[to_additive] lemma isCoveringMapOn_of_properlyDiscontinuousSmul
    [ProperlyDiscontinuousSMul G E] [WeaklyLocallyCompactSpace E] [T2Space E] :
    IsCoveringMapOn p (p '' {e | MulAction.stabilizer G e = ‚ä•}) :=
  hp.isCoveringMapOn_of_mulAction hpG (ProperlyDiscontinuousSMul.disjoint_image_nhds G)

@[to_additive] lemma _root_.isCoveringMapOn_quotient_of_properlyDiscontinuousSmul
    [ProperlyDiscontinuousSMul G E] [WeaklyLocallyCompactSpace E] [T2Space E] :
    IsCoveringMapOn (Quotient.mk _) <|
      (Quotient.mk <| MulAction.orbitRel G E) '' {e | MulAction.stabilizer G e = ‚ä•} :=
  quotientMap_quotient_mk'.isCoveringMapOn_of_properlyDiscontinuousSmul Quotient.eq''

end QuotientMap

@[to_additive] lemma Subgroup.isCoveringMap {G} [Group G] [TopologicalSpace G]
    [TopologicalGroup G] (S : Subgroup G) [DiscreteTopology S] :
    IsCoveringMap (QuotientGroup.mk (s := S)) := by
  obtain ‚ü®U, hU, disj‚ü© := S.disjoint_nhds_of_discrete
  refine quotientMap_quotient_mk'.isCoveringMap_of_mulAction (G := S.op) Quotient.eq'' fun g ‚Ü¶ ?_
  refine ‚ü®_, singleton_mul_mem_nhds_of_nhds_one g hU, fun ‚ü®‚ü®s‚ü©, hS‚ü© hs ‚Ü¶ Subtype.ext <|
    MulOpposite.unop_injective <| disj _ hS <| Or.inr ?_‚ü©
  simp_rw [‚Üê Set.nonempty_iff_ne_empty] at hs ‚ä¢
  obtain ‚ü®_, ‚ü®_, ‚ü®_, x, rfl, hx, rfl‚ü©, rfl‚ü©, g, y, rfl, hy, he‚ü© := hs
  exact ‚ü®y, ‚ü®x, hx, mul_left_cancel (he.trans <| mul_assoc _ _ _).symm‚ü©, hy‚ü©

theorem isCoveringMap_coe_addCircle (p : ‚Ñù) : IsCoveringMap ((‚Üë) : ‚Ñù ‚Üí AddCircle p) :=
  AddSubgroup.isCoveringMap _
