/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro, Patrick Massot
-/
import Mathlib.Topology.Homeomorph.Defs
import Mathlib.Topology.Maps.Basic

/-!
# Disjoint unions and products of topological spaces

This file constructs sums (disjoint unions) and products of topological spaces
and sets up their basic theory, such as criteria for maps into or out of these
constructions to be continuous; descriptions of the open sets, neighborhood filters,
and generators of these constructions; and their behavior with respect to embeddings
and other specific classes of maps.

We also provide basic homeomorphisms, to show that sums and products are commutative, associative
and distributive (up to homeomorphism).

## Implementation note

The constructed topologies are defined using induced and coinduced topologies
along with the complete lattice structure on topologies. Their universal properties
(for example, a map `X ‚Üí Y √ó Z` is continuous if and only if both projections
`X ‚Üí Y`, `X ‚Üí Z` are) follow easily using order-theoretic descriptions of
continuity. With more work we can also extract descriptions of the open sets,
neighborhood filters and so on.

## Tags

product, sum, disjoint union

-/

noncomputable section

open Topology TopologicalSpace Set Filter Function

universe u v u' v'

variable {X : Type u} {Y : Type v} {W Z Œµ Œ∂ : Type*}

instance instTopologicalSpaceSum [t‚ÇÅ : TopologicalSpace X] [t‚ÇÇ : TopologicalSpace Y] :
    TopologicalSpace (X ‚äï Y) :=
  coinduced Sum.inl t‚ÇÅ ‚äî coinduced Sum.inr t‚ÇÇ

instance instTopologicalSpaceProd [t‚ÇÅ : TopologicalSpace X] [t‚ÇÇ : TopologicalSpace Y] :
    TopologicalSpace (X √ó Y) :=
  induced Prod.fst t‚ÇÅ ‚äì induced Prod.snd t‚ÇÇ

section Prod

variable [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z] [TopologicalSpace W]
  [TopologicalSpace Œµ] [TopologicalSpace Œ∂]

@[simp]
theorem continuous_prodMk {f : X ‚Üí Y} {g : X ‚Üí Z} :
    (Continuous fun x => (f x, g x)) ‚Üî Continuous f ‚àß Continuous g :=
  continuous_inf_rng.trans <| continuous_induced_rng.and continuous_induced_rng

@[deprecated (since := "2025-03-10")]
alias continuous_prod_mk := continuous_prodMk

@[continuity]
theorem continuous_fst : Continuous (@Prod.fst X Y) :=
  (continuous_prodMk.1 continuous_id).1

/-- Postcomposing `f` with `Prod.fst` is continuous -/
@[fun_prop]
theorem Continuous.fst {f : X ‚Üí Y √ó Z} (hf : Continuous f) : Continuous fun x : X => (f x).1 :=
  continuous_fst.comp hf

/-- Precomposing `f` with `Prod.fst` is continuous -/
theorem Continuous.fst' {f : X ‚Üí Z} (hf : Continuous f) : Continuous fun x : X √ó Y => f x.fst :=
  hf.comp continuous_fst

theorem continuousAt_fst {p : X √ó Y} : ContinuousAt Prod.fst p :=
  continuous_fst.continuousAt

/-- Postcomposing `f` with `Prod.fst` is continuous at `x` -/
@[fun_prop]
theorem ContinuousAt.fst {f : X ‚Üí Y √ó Z} {x : X} (hf : ContinuousAt f x) :
    ContinuousAt (fun x : X => (f x).1) x :=
  continuousAt_fst.comp hf

/-- Precomposing `f` with `Prod.fst` is continuous at `(x, y)` -/
theorem ContinuousAt.fst' {f : X ‚Üí Z} {x : X} {y : Y} (hf : ContinuousAt f x) :
    ContinuousAt (fun x : X √ó Y => f x.fst) (x, y) :=
  ContinuousAt.comp hf continuousAt_fst

/-- Precomposing `f` with `Prod.fst` is continuous at `x : X √ó Y` -/
theorem ContinuousAt.fst'' {f : X ‚Üí Z} {x : X √ó Y} (hf : ContinuousAt f x.fst) :
    ContinuousAt (fun x : X √ó Y => f x.fst) x :=
  hf.comp continuousAt_fst

theorem Filter.Tendsto.fst_nhds {X} {l : Filter X} {f : X ‚Üí Y √ó Z} {p : Y √ó Z}
    (h : Tendsto f l (ùìù p)) : Tendsto (fun a ‚Ü¶ (f a).1) l (ùìù <| p.1) :=
  continuousAt_fst.tendsto.comp h

@[continuity]
theorem continuous_snd : Continuous (@Prod.snd X Y) :=
  (continuous_prodMk.1 continuous_id).2

/-- Postcomposing `f` with `Prod.snd` is continuous -/
@[fun_prop]
theorem Continuous.snd {f : X ‚Üí Y √ó Z} (hf : Continuous f) : Continuous fun x : X => (f x).2 :=
  continuous_snd.comp hf

/-- Precomposing `f` with `Prod.snd` is continuous -/
theorem Continuous.snd' {f : Y ‚Üí Z} (hf : Continuous f) : Continuous fun x : X √ó Y => f x.snd :=
  hf.comp continuous_snd

theorem continuousAt_snd {p : X √ó Y} : ContinuousAt Prod.snd p :=
  continuous_snd.continuousAt

/-- Postcomposing `f` with `Prod.snd` is continuous at `x` -/
@[fun_prop]
theorem ContinuousAt.snd {f : X ‚Üí Y √ó Z} {x : X} (hf : ContinuousAt f x) :
    ContinuousAt (fun x : X => (f x).2) x :=
  continuousAt_snd.comp hf

/-- Precomposing `f` with `Prod.snd` is continuous at `(x, y)` -/
theorem ContinuousAt.snd' {f : Y ‚Üí Z} {x : X} {y : Y} (hf : ContinuousAt f y) :
    ContinuousAt (fun x : X √ó Y => f x.snd) (x, y) :=
  ContinuousAt.comp hf continuousAt_snd

/-- Precomposing `f` with `Prod.snd` is continuous at `x : X √ó Y` -/
theorem ContinuousAt.snd'' {f : Y ‚Üí Z} {x : X √ó Y} (hf : ContinuousAt f x.snd) :
    ContinuousAt (fun x : X √ó Y => f x.snd) x :=
  hf.comp continuousAt_snd

theorem Filter.Tendsto.snd_nhds {X} {l : Filter X} {f : X ‚Üí Y √ó Z} {p : Y √ó Z}
    (h : Tendsto f l (ùìù p)) : Tendsto (fun a ‚Ü¶ (f a).2) l (ùìù <| p.2) :=
  continuousAt_snd.tendsto.comp h

@[continuity, fun_prop]
theorem Continuous.prodMk {f : Z ‚Üí X} {g : Z ‚Üí Y} (hf : Continuous f) (hg : Continuous g) :
    Continuous fun x => (f x, g x) :=
  continuous_prodMk.2 ‚ü®hf, hg‚ü©

@[deprecated (since := "2025-03-10")]
alias Continuous.prod_mk := Continuous.prodMk

@[continuity]
theorem Continuous.prodMk_right (x : X) : Continuous fun y : Y => (x, y) := by fun_prop

@[deprecated (since := "2025-03-10")]
alias Continuous.Prod.mk := Continuous.prodMk_right

@[continuity]
theorem Continuous.prodMk_left (y : Y) : Continuous fun x : X => (x, y) := by fun_prop

@[deprecated (since := "2025-03-10")]
alias Continuous.Prod.mk_left := Continuous.prodMk_left

/-- If `f x y` is continuous in `x` for all `y ‚àà s`,
then the set of `x` such that `f x` maps `s` to `t` is closed. -/
lemma IsClosed.setOf_mapsTo {Œ± : Type*} {f : X ‚Üí Œ± ‚Üí Z} {s : Set Œ±} {t : Set Z} (ht : IsClosed t)
    (hf : ‚àÄ a ‚àà s, Continuous (f ¬∑ a)) : IsClosed {x | MapsTo (f x) s t} := by
  simpa only [MapsTo, setOf_forall] using isClosed_biInter fun y hy ‚Ü¶ ht.preimage (hf y hy)

theorem Continuous.comp‚ÇÇ {g : X √ó Y ‚Üí Z} (hg : Continuous g) {e : W ‚Üí X} (he : Continuous e)
    {f : W ‚Üí Y} (hf : Continuous f) : Continuous fun w => g (e w, f w) :=
  hg.comp <| he.prodMk hf

theorem Continuous.comp‚ÇÉ {g : X √ó Y √ó Z ‚Üí Œµ} (hg : Continuous g) {e : W ‚Üí X} (he : Continuous e)
    {f : W ‚Üí Y} (hf : Continuous f) {k : W ‚Üí Z} (hk : Continuous k) :
    Continuous fun w => g (e w, f w, k w) :=
  hg.comp‚ÇÇ he <| hf.prodMk hk

theorem Continuous.comp‚ÇÑ {g : X √ó Y √ó Z √ó Œ∂ ‚Üí Œµ} (hg : Continuous g) {e : W ‚Üí X} (he : Continuous e)
    {f : W ‚Üí Y} (hf : Continuous f) {k : W ‚Üí Z} (hk : Continuous k) {l : W ‚Üí Œ∂}
    (hl : Continuous l) : Continuous fun w => g (e w, f w, k w, l w) :=
  hg.comp‚ÇÉ he hf <| hk.prodMk hl

@[continuity, fun_prop]
theorem Continuous.prodMap {f : Z ‚Üí X} {g : W ‚Üí Y} (hf : Continuous f) (hg : Continuous g) :
    Continuous (Prod.map f g) :=
  hf.fst'.prodMk hg.snd'

/-- A version of `continuous_inf_dom_left` for binary functions -/
theorem continuous_inf_dom_left‚ÇÇ {X Y Z} {f : X ‚Üí Y ‚Üí Z} {ta1 ta2 : TopologicalSpace X}
    {tb1 tb2 : TopologicalSpace Y} {tc1 : TopologicalSpace Z}
    (h : by haveI := ta1; haveI := tb1; exact Continuous fun p : X √ó Y => f p.1 p.2) : by
    haveI := ta1 ‚äì ta2; haveI := tb1 ‚äì tb2; exact Continuous fun p : X √ó Y => f p.1 p.2 := by
  have ha := @continuous_inf_dom_left _ _ id ta1 ta2 ta1 (@continuous_id _ (id _))
  have hb := @continuous_inf_dom_left _ _ id tb1 tb2 tb1 (@continuous_id _ (id _))
  have h_continuous_id := @Continuous.prodMap _ _ _ _ ta1 tb1 (ta1 ‚äì ta2) (tb1 ‚äì tb2) _ _ ha hb
  exact @Continuous.comp _ _ _ (id _) (id _) _ _ _ h h_continuous_id

/-- A version of `continuous_inf_dom_right` for binary functions -/
theorem continuous_inf_dom_right‚ÇÇ {X Y Z} {f : X ‚Üí Y ‚Üí Z} {ta1 ta2 : TopologicalSpace X}
    {tb1 tb2 : TopologicalSpace Y} {tc1 : TopologicalSpace Z}
    (h : by haveI := ta2; haveI := tb2; exact Continuous fun p : X √ó Y => f p.1 p.2) : by
    haveI := ta1 ‚äì ta2; haveI := tb1 ‚äì tb2; exact Continuous fun p : X √ó Y => f p.1 p.2 := by
  have ha := @continuous_inf_dom_right _ _ id ta1 ta2 ta2 (@continuous_id _ (id _))
  have hb := @continuous_inf_dom_right _ _ id tb1 tb2 tb2 (@continuous_id _ (id _))
  have h_continuous_id := @Continuous.prodMap _ _ _ _ ta2 tb2 (ta1 ‚äì ta2) (tb1 ‚äì tb2) _ _ ha hb
  exact @Continuous.comp _ _ _ (id _) (id _) _ _ _ h h_continuous_id

/-- A version of `continuous_sInf_dom` for binary functions -/
theorem continuous_sInf_dom‚ÇÇ {X Y Z} {f : X ‚Üí Y ‚Üí Z} {tas : Set (TopologicalSpace X)}
    {tbs : Set (TopologicalSpace Y)} {tX : TopologicalSpace X} {tY : TopologicalSpace Y}
    {tc : TopologicalSpace Z} (hX : tX ‚àà tas) (hY : tY ‚àà tbs)
    (hf : Continuous fun p : X √ó Y => f p.1 p.2) : by
    haveI := sInf tas; haveI := sInf tbs
    exact @Continuous _ _ _ tc fun p : X √ó Y => f p.1 p.2 := by
  have hX := continuous_sInf_dom hX continuous_id
  have hY := continuous_sInf_dom hY continuous_id
  have h_continuous_id := @Continuous.prodMap _ _ _ _ tX tY (sInf tas) (sInf tbs) _ _ hX hY
  exact @Continuous.comp _ _ _ (id _) (id _) _ _ _ hf h_continuous_id

theorem Filter.Eventually.prod_inl_nhds {p : X ‚Üí Prop} {x : X} (h : ‚àÄ·∂† x in ùìù x, p x) (y : Y) :
    ‚àÄ·∂† x in ùìù (x, y), p (x : X √ó Y).1 :=
  continuousAt_fst h

theorem Filter.Eventually.prod_inr_nhds {p : Y ‚Üí Prop} {y : Y} (h : ‚àÄ·∂† x in ùìù y, p x) (x : X) :
    ‚àÄ·∂† x in ùìù (x, y), p (x : X √ó Y).2 :=
  continuousAt_snd h

theorem Filter.Eventually.prodMk_nhds {px : X ‚Üí Prop} {x} (hx : ‚àÄ·∂† x in ùìù x, px x) {py : Y ‚Üí Prop}
    {y} (hy : ‚àÄ·∂† y in ùìù y, py y) : ‚àÄ·∂† p in ùìù (x, y), px (p : X √ó Y).1 ‚àß py p.2 :=
  (hx.prod_inl_nhds y).and (hy.prod_inr_nhds x)

@[deprecated (since := "2025-03-10")]
alias Filter.Eventually.prod_mk_nhds := Filter.Eventually.prodMk_nhds

theorem continuous_swap : Continuous (Prod.swap : X √ó Y ‚Üí Y √ó X) :=
  continuous_snd.prodMk continuous_fst

lemma isClosedMap_swap : IsClosedMap (Prod.swap : X √ó Y ‚Üí Y √ó X) := fun s hs ‚Ü¶ by
  rw [image_swap_eq_preimage_swap]
  exact hs.preimage continuous_swap

theorem Continuous.uncurry_left {f : X ‚Üí Y ‚Üí Z} (x : X) (h : Continuous (uncurry f)) :
    Continuous (f x) :=
  h.comp (.prodMk_right _)

theorem Continuous.uncurry_right {f : X ‚Üí Y ‚Üí Z} (y : Y) (h : Continuous (uncurry f)) :
    Continuous fun a => f a y :=
  h.comp (.prodMk_left _)

theorem continuous_curry {g : X √ó Y ‚Üí Z} (x : X) (h : Continuous g) : Continuous (curry g x) :=
  Continuous.uncurry_left x h

theorem IsOpen.prod {s : Set X} {t : Set Y} (hs : IsOpen s) (ht : IsOpen t) : IsOpen (s √óÀ¢ t) :=
  (hs.preimage continuous_fst).inter (ht.preimage continuous_snd)

-- Porting note (https://github.com/leanprover-community/mathlib4/issues/11215): TODO: Lean fails to find `t‚ÇÅ` and `t‚ÇÇ` by unification
theorem nhds_prod_eq {x : X} {y : Y} : ùìù (x, y) = ùìù x √óÀ¢ ùìù y := by
  rw [prod_eq_inf, instTopologicalSpaceProd, nhds_inf (t‚ÇÅ := TopologicalSpace.induced Prod.fst _)
    (t‚ÇÇ := TopologicalSpace.induced Prod.snd _), nhds_induced, nhds_induced]

theorem nhdsWithin_prod_eq (x : X) (y : Y) (s : Set X) (t : Set Y) :
    ùìù[s √óÀ¢ t] (x, y) = ùìù[s] x √óÀ¢ ùìù[t] y := by
  simp only [nhdsWithin, nhds_prod_eq, ‚Üê prod_inf_prod, prod_principal_principal]

instance Prod.instNeBotNhdsWithinIio [Preorder X] [Preorder Y] {x : X √ó Y}
    [hx‚ÇÅ : (ùìù[<] x.1).NeBot] [hx‚ÇÇ : (ùìù[<] x.2).NeBot] : (ùìù[<] x).NeBot := by
  refine (hx‚ÇÅ.prod hx‚ÇÇ).mono ?_
  rw [‚Üê nhdsWithin_prod_eq]
  exact nhdsWithin_mono _ fun _ ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚Ü¶ Prod.lt_iff.2 <| .inl ‚ü®h‚ÇÅ, h‚ÇÇ.le‚ü©

instance Prod.instNeBotNhdsWithinIoi [Preorder X] [Preorder Y] {x : X √ó Y}
    [hx‚ÇÅ : (ùìù[>] x.1).NeBot] [hx‚ÇÇ : (ùìù[>] x.2).NeBot] : (ùìù[>] x).NeBot := by
  refine (hx‚ÇÅ.prod hx‚ÇÇ).mono ?_
  rw [‚Üê nhdsWithin_prod_eq]
  exact nhdsWithin_mono _ fun _ ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚Ü¶ Prod.lt_iff.2 <| .inl ‚ü®h‚ÇÅ, h‚ÇÇ.le‚ü©

theorem mem_nhds_prod_iff {x : X} {y : Y} {s : Set (X √ó Y)} :
    s ‚àà ùìù (x, y) ‚Üî ‚àÉ u ‚àà ùìù x, ‚àÉ v ‚àà ùìù y, u √óÀ¢ v ‚äÜ s := by rw [nhds_prod_eq, mem_prod_iff]

theorem mem_nhdsWithin_prod_iff {x : X} {y : Y} {s : Set (X √ó Y)} {tx : Set X} {ty : Set Y} :
    s ‚àà ùìù[tx √óÀ¢ ty] (x, y) ‚Üî ‚àÉ u ‚àà ùìù[tx] x, ‚àÉ v ‚àà ùìù[ty] y, u √óÀ¢ v ‚äÜ s := by
  rw [nhdsWithin_prod_eq, mem_prod_iff]

theorem Filter.HasBasis.prod_nhds {ŒπX ŒπY : Type*} {px : ŒπX ‚Üí Prop} {py : ŒπY ‚Üí Prop}
    {sx : ŒπX ‚Üí Set X} {sy : ŒπY ‚Üí Set Y} {x : X} {y : Y} (hx : (ùìù x).HasBasis px sx)
    (hy : (ùìù y).HasBasis py sy) :
    (ùìù (x, y)).HasBasis (fun i : ŒπX √ó ŒπY => px i.1 ‚àß py i.2) fun i => sx i.1 √óÀ¢ sy i.2 := by
  rw [nhds_prod_eq]
  exact hx.prod hy

theorem Filter.HasBasis.prod_nhds' {ŒπX ŒπY : Type*} {pX : ŒπX ‚Üí Prop} {pY : ŒπY ‚Üí Prop}
    {sx : ŒπX ‚Üí Set X} {sy : ŒπY ‚Üí Set Y} {p : X √ó Y} (hx : (ùìù p.1).HasBasis pX sx)
    (hy : (ùìù p.2).HasBasis pY sy) :
    (ùìù p).HasBasis (fun i : ŒπX √ó ŒπY => pX i.1 ‚àß pY i.2) fun i => sx i.1 √óÀ¢ sy i.2 :=
  hx.prod_nhds hy

theorem mem_nhds_prod_iff' {x : X} {y : Y} {s : Set (X √ó Y)} :
    s ‚àà ùìù (x, y) ‚Üî ‚àÉ u v, IsOpen u ‚àß x ‚àà u ‚àß IsOpen v ‚àß y ‚àà v ‚àß u √óÀ¢ v ‚äÜ s :=
  ((nhds_basis_opens x).prod_nhds (nhds_basis_opens y)).mem_iff.trans <| by
    simp only [Prod.exists, and_comm, and_assoc, and_left_comm]

theorem Prod.tendsto_iff {X} (seq : X ‚Üí Y √ó Z) {f : Filter X} (p : Y √ó Z) :
    Tendsto seq f (ùìù p) ‚Üî
      Tendsto (fun n => (seq n).fst) f (ùìù p.fst) ‚àß Tendsto (fun n => (seq n).snd) f (ùìù p.snd) := by
  rw [nhds_prod_eq, Filter.tendsto_prod_iff']

instance [DiscreteTopology X] [DiscreteTopology Y] : DiscreteTopology (X √ó Y) :=
  discreteTopology_iff_nhds.2 fun (a, b) => by
    rw [nhds_prod_eq, nhds_discrete X, nhds_discrete Y, prod_pure_pure]

theorem prod_mem_nhds_iff {s : Set X} {t : Set Y} {x : X} {y : Y} :
    s √óÀ¢ t ‚àà ùìù (x, y) ‚Üî s ‚àà ùìù x ‚àß t ‚àà ùìù y := by rw [nhds_prod_eq, prod_mem_prod_iff]

theorem prod_mem_nhds {s : Set X} {t : Set Y} {x : X} {y : Y} (hx : s ‚àà ùìù x) (hy : t ‚àà ùìù y) :
    s √óÀ¢ t ‚àà ùìù (x, y) :=
  prod_mem_nhds_iff.2 ‚ü®hx, hy‚ü©

theorem isOpen_setOf_disjoint_nhds_nhds : IsOpen { p : X √ó X | Disjoint (ùìù p.1) (ùìù p.2) } := by
  simp only [isOpen_iff_mem_nhds, Prod.forall, mem_setOf_eq]
  intro x y h
  obtain ‚ü®U, hU, V, hV, hd‚ü© := ((nhds_basis_opens x).disjoint_iff (nhds_basis_opens y)).mp h
  exact mem_nhds_prod_iff'.mpr ‚ü®U, V, hU.2, hU.1, hV.2, hV.1, fun ‚ü®x', y'‚ü© ‚ü®hx', hy'‚ü© =>
    disjoint_of_disjoint_of_mem hd (hU.2.mem_nhds hx') (hV.2.mem_nhds hy')‚ü©

theorem Filter.Eventually.prod_nhds {p : X ‚Üí Prop} {q : Y ‚Üí Prop} {x : X} {y : Y}
    (hx : ‚àÄ·∂† x in ùìù x, p x) (hy : ‚àÄ·∂† y in ùìù y, q y) : ‚àÄ·∂† z : X √ó Y in ùìù (x, y), p z.1 ‚àß q z.2 :=
  prod_mem_nhds hx hy

theorem Filter.EventuallyEq.prodMap_nhds {Œ± Œ≤ : Type*} {f‚ÇÅ f‚ÇÇ : X ‚Üí Œ±} {g‚ÇÅ g‚ÇÇ : Y ‚Üí Œ≤}
    {x : X} {y : Y} (hf : f‚ÇÅ =·∂†[ùìù x] f‚ÇÇ) (hg : g‚ÇÅ =·∂†[ùìù y] g‚ÇÇ) :
    Prod.map f‚ÇÅ g‚ÇÅ =·∂†[ùìù (x, y)] Prod.map f‚ÇÇ g‚ÇÇ := by
  rw [nhds_prod_eq]
  exact hf.prodMap hg

theorem Filter.EventuallyLE.prodMap_nhds {Œ± Œ≤ : Type*} [LE Œ±] [LE Œ≤] {f‚ÇÅ f‚ÇÇ : X ‚Üí Œ±} {g‚ÇÅ g‚ÇÇ : Y ‚Üí Œ≤}
    {x : X} {y : Y} (hf : f‚ÇÅ ‚â§·∂†[ùìù x] f‚ÇÇ) (hg : g‚ÇÅ ‚â§·∂†[ùìù y] g‚ÇÇ) :
    Prod.map f‚ÇÅ g‚ÇÅ ‚â§·∂†[ùìù (x, y)] Prod.map f‚ÇÇ g‚ÇÇ := by
  rw [nhds_prod_eq]
  exact hf.prodMap hg

theorem nhds_swap (x : X) (y : Y) : ùìù (x, y) = (ùìù (y, x)).map Prod.swap := by
  rw [nhds_prod_eq, Filter.prod_comm, nhds_prod_eq]; rfl

theorem Filter.Tendsto.prodMk_nhds {Œ≥} {x : X} {y : Y} {f : Filter Œ≥} {mx : Œ≥ ‚Üí X} {my : Œ≥ ‚Üí Y}
    (hx : Tendsto mx f (ùìù x)) (hy : Tendsto my f (ùìù y)) :
    Tendsto (fun c => (mx c, my c)) f (ùìù (x, y)) := by
  rw [nhds_prod_eq]
  exact hx.prodMk hy

@[deprecated (since := "2025-03-10")]
alias Filter.Tendsto.prod_mk_nhds := Filter.Tendsto.prodMk_nhds

theorem Filter.Tendsto.prodMap_nhds {x : X} {y : Y} {z : Z} {w : W} {f : X ‚Üí Y} {g : Z ‚Üí W}
    (hf : Tendsto f (ùìù x) (ùìù y)) (hg : Tendsto g (ùìù z) (ùìù w)) :
    Tendsto (Prod.map f g) (ùìù (x, z)) (ùìù (y, w)) := by
  rw [nhds_prod_eq, nhds_prod_eq]
  exact hf.prodMap hg

theorem Filter.Eventually.curry_nhds {p : X √ó Y ‚Üí Prop} {x : X} {y : Y}
    (h : ‚àÄ·∂† x in ùìù (x, y), p x) : ‚àÄ·∂† x' in ùìù x, ‚àÄ·∂† y' in ùìù y, p (x', y') := by
  rw [nhds_prod_eq] at h
  exact h.curry

@[fun_prop]
theorem ContinuousAt.prodMk {f : X ‚Üí Y} {g : X ‚Üí Z} {x : X} (hf : ContinuousAt f x)
    (hg : ContinuousAt g x) : ContinuousAt (fun x => (f x, g x)) x :=
  hf.prodMk_nhds hg

@[deprecated (since := "2025-03-10")]
alias ContinuousAt.prod := ContinuousAt.prodMk

theorem ContinuousAt.prodMap {f : X ‚Üí Z} {g : Y ‚Üí W} {p : X √ó Y} (hf : ContinuousAt f p.fst)
    (hg : ContinuousAt g p.snd) : ContinuousAt (Prod.map f g) p :=
  hf.fst''.prodMk hg.snd''

/-- A version of `ContinuousAt.prodMap` that avoids `Prod.fst`/`Prod.snd`
by assuming that the point is `(x, y)`. -/
theorem ContinuousAt.prodMap' {f : X ‚Üí Z} {g : Y ‚Üí W} {x : X} {y : Y} (hf : ContinuousAt f x)
    (hg : ContinuousAt g y) : ContinuousAt (Prod.map f g) (x, y) :=
  hf.prodMap hg

theorem ContinuousAt.comp‚ÇÇ {f : Y √ó Z ‚Üí W} {g : X ‚Üí Y} {h : X ‚Üí Z} {x : X}
    (hf : ContinuousAt f (g x, h x)) (hg : ContinuousAt g x) (hh : ContinuousAt h x) :
    ContinuousAt (fun x ‚Ü¶ f (g x, h x)) x :=
  ContinuousAt.comp hf (hg.prodMk hh)

theorem ContinuousAt.comp‚ÇÇ_of_eq {f : Y √ó Z ‚Üí W} {g : X ‚Üí Y} {h : X ‚Üí Z} {x : X} {y : Y √ó Z}
    (hf : ContinuousAt f y) (hg : ContinuousAt g x) (hh : ContinuousAt h x) (e : (g x, h x) = y) :
    ContinuousAt (fun x ‚Ü¶ f (g x, h x)) x := by
  rw [‚Üê e] at hf
  exact hf.comp‚ÇÇ hg hh

/-- Continuous functions on products are continuous in their first argument -/
theorem Continuous.curry_left {f : X √ó Y ‚Üí Z} (hf : Continuous f) {y : Y} :
    Continuous fun x ‚Ü¶ f (x, y) :=
  hf.comp (.prodMk_left _)
alias Continuous.along_fst := Continuous.curry_left

/-- Continuous functions on products are continuous in their second argument -/
theorem Continuous.curry_right {f : X √ó Y ‚Üí Z} (hf : Continuous f) {x : X} :
    Continuous fun y ‚Ü¶ f (x, y) :=
  hf.comp (.prodMk_right _)
alias Continuous.along_snd := Continuous.curry_right

-- todo: prove a version of `generateFrom_union` with `image2 (‚à©) s t` in the LHS and use it here
theorem prod_generateFrom_generateFrom_eq {X Y : Type*} {s : Set (Set X)} {t : Set (Set Y)}
    (hs : ‚ãÉ‚ÇÄ s = univ) (ht : ‚ãÉ‚ÇÄ t = univ) :
    @instTopologicalSpaceProd X Y (generateFrom s) (generateFrom t) =
      generateFrom (image2 (¬∑  √óÀ¢ ¬∑) s t) :=
  let G := generateFrom (image2  (¬∑  √óÀ¢ ¬∑) s t)
  le_antisymm
    (le_generateFrom fun _ ‚ü®_, hu, _, hv, g_eq‚ü© =>
      g_eq.symm ‚ñ∏
        @IsOpen.prod _ _ (generateFrom s) (generateFrom t) _ _ (GenerateOpen.basic _ hu)
          (GenerateOpen.basic _ hv))
    (le_inf
      (coinduced_le_iff_le_induced.mp <|
        le_generateFrom fun u hu =>
          have : ‚ãÉ v ‚àà t, u √óÀ¢ v = Prod.fst ‚Åª¬π' u := by
            simp_rw [‚Üê prod_iUnion, ‚Üê sUnion_eq_biUnion, ht, prod_univ]
          show G.IsOpen (Prod.fst ‚Åª¬π' u) by
            rw [‚Üê this]
            exact
              isOpen_iUnion fun v =>
                isOpen_iUnion fun hv => GenerateOpen.basic _ ‚ü®_, hu, _, hv, rfl‚ü©)
      (coinduced_le_iff_le_induced.mp <|
        le_generateFrom fun v hv =>
          have : ‚ãÉ u ‚àà s, u √óÀ¢ v = Prod.snd ‚Åª¬π' v := by
            simp_rw [‚Üê iUnion_prod_const, ‚Üê sUnion_eq_biUnion, hs, univ_prod]
          show G.IsOpen (Prod.snd ‚Åª¬π' v) by
            rw [‚Üê this]
            exact
              isOpen_iUnion fun u =>
                isOpen_iUnion fun hu => GenerateOpen.basic _ ‚ü®_, hu, _, hv, rfl‚ü©))

theorem prod_eq_generateFrom :
    instTopologicalSpaceProd =
      generateFrom { g | ‚àÉ (s : Set X) (t : Set Y), IsOpen s ‚àß IsOpen t ‚àß g = s √óÀ¢ t } :=
  le_antisymm (le_generateFrom fun _ ‚ü®_, _, hs, ht, g_eq‚ü© => g_eq.symm ‚ñ∏ hs.prod ht)
    (le_inf
      (forall_mem_image.2 fun t ht =>
        GenerateOpen.basic _ ‚ü®t, univ, by simpa [Set.prod_eq] using ht‚ü©)
      (forall_mem_image.2 fun t ht =>
        GenerateOpen.basic _ ‚ü®univ, t, by simpa [Set.prod_eq] using ht‚ü©))

-- TODO: align with `mem_nhds_prod_iff'`
theorem isOpen_prod_iff {s : Set (X √ó Y)} :
    IsOpen s ‚Üî ‚àÄ a b, (a, b) ‚àà s ‚Üí
      ‚àÉ u v, IsOpen u ‚àß IsOpen v ‚àß a ‚àà u ‚àß b ‚àà v ‚àß u √óÀ¢ v ‚äÜ s :=
  isOpen_iff_mem_nhds.trans <| by simp_rw [Prod.forall, mem_nhds_prod_iff', and_left_comm]

/-- A product of induced topologies is induced by the product map -/
theorem prod_induced_induced {X Z} (f : X ‚Üí Y) (g : Z ‚Üí W) :
    @instTopologicalSpaceProd X Z (induced f ‚Äπ_‚Ä∫) (induced g ‚Äπ_‚Ä∫) =
      induced (fun p => (f p.1, g p.2)) instTopologicalSpaceProd := by
  delta instTopologicalSpaceProd
  simp_rw [induced_inf, induced_compose]
  rfl

/-- Given a neighborhood `s` of `(x, x)`, then `(x, x)` has a square open neighborhood
  that is a subset of `s`. -/
theorem exists_nhds_square {s : Set (X √ó X)} {x : X} (hx : s ‚àà ùìù (x, x)) :
    ‚àÉ U : Set X, IsOpen U ‚àß x ‚àà U ‚àß U √óÀ¢ U ‚äÜ s := by
  simpa [nhds_prod_eq, (nhds_basis_opens x).prod_self.mem_iff, and_assoc, and_left_comm] using hx

/-- `Prod.fst` maps neighborhood of `x : X √ó Y` within the section `Prod.snd ‚Åª¬π' {x.2}`
to `ùìù x.1`. -/
theorem map_fst_nhdsWithin (x : X √ó Y) : map Prod.fst (ùìù[Prod.snd ‚Åª¬π' {x.2}] x) = ùìù x.1 := by
  refine le_antisymm (continuousAt_fst.mono_left inf_le_left) fun s hs => ?_
  rcases x with ‚ü®x, y‚ü©
  rw [mem_map, nhdsWithin, mem_inf_principal, mem_nhds_prod_iff] at hs
  rcases hs with ‚ü®u, hu, v, hv, H‚ü©
  simp only [prod_subset_iff, mem_singleton_iff, mem_setOf_eq, mem_preimage] at H
  exact mem_of_superset hu fun z hz => H _ hz _ (mem_of_mem_nhds hv) rfl

@[simp]
theorem map_fst_nhds (x : X √ó Y) : map Prod.fst (ùìù x) = ùìù x.1 :=
  le_antisymm continuousAt_fst <| (map_fst_nhdsWithin x).symm.trans_le (map_mono inf_le_left)

/-- The first projection in a product of topological spaces sends open sets to open sets. -/
theorem isOpenMap_fst : IsOpenMap (@Prod.fst X Y) :=
  isOpenMap_iff_nhds_le.2 fun x => (map_fst_nhds x).ge

/-- `Prod.snd` maps neighborhood of `x : X √ó Y` within the section `Prod.fst ‚Åª¬π' {x.1}`
to `ùìù x.2`. -/
theorem map_snd_nhdsWithin (x : X √ó Y) : map Prod.snd (ùìù[Prod.fst ‚Åª¬π' {x.1}] x) = ùìù x.2 := by
  refine le_antisymm (continuousAt_snd.mono_left inf_le_left) fun s hs => ?_
  rcases x with ‚ü®x, y‚ü©
  rw [mem_map, nhdsWithin, mem_inf_principal, mem_nhds_prod_iff] at hs
  rcases hs with ‚ü®u, hu, v, hv, H‚ü©
  simp only [prod_subset_iff, mem_singleton_iff, mem_setOf_eq, mem_preimage] at H
  exact mem_of_superset hv fun z hz => H _ (mem_of_mem_nhds hu) _ hz rfl

@[simp]
theorem map_snd_nhds (x : X √ó Y) : map Prod.snd (ùìù x) = ùìù x.2 :=
  le_antisymm continuousAt_snd <| (map_snd_nhdsWithin x).symm.trans_le (map_mono inf_le_left)

/-- The second projection in a product of topological spaces sends open sets to open sets. -/
theorem isOpenMap_snd : IsOpenMap (@Prod.snd X Y) :=
  isOpenMap_iff_nhds_le.2 fun x => (map_snd_nhds x).ge

/-- A product set is open in a product space if and only if each factor is open, or one of them is
empty -/
theorem isOpen_prod_iff' {s : Set X} {t : Set Y} :
    IsOpen (s √óÀ¢ t) ‚Üî IsOpen s ‚àß IsOpen t ‚à® s = ‚àÖ ‚à® t = ‚àÖ := by
  rcases (s √óÀ¢ t).eq_empty_or_nonempty with h | h
  ¬∑ simp [h, prod_eq_empty_iff.1 h]
  ¬∑ have st : s.Nonempty ‚àß t.Nonempty := prod_nonempty_iff.1 h
    constructor
    ¬∑ intro (H : IsOpen (s √óÀ¢ t))
      refine Or.inl ‚ü®?_, ?_‚ü©
      ¬∑ simpa only [fst_image_prod _ st.2] using isOpenMap_fst _ H
      ¬∑ simpa only [snd_image_prod st.1 t] using isOpenMap_snd _ H
    ¬∑ intro H
      simp only [st.1.ne_empty, st.2.ne_empty, not_false_iff, or_false] at H
      exact H.1.prod H.2

theorem isQuotientMap_fst [Nonempty Y] : IsQuotientMap (Prod.fst : X √ó Y ‚Üí X) :=
  isOpenMap_fst.isQuotientMap continuous_fst Prod.fst_surjective

@[deprecated (since := "2024-10-22")]
alias quotientMap_fst := isQuotientMap_fst

theorem isQuotientMap_snd [Nonempty X] : IsQuotientMap (Prod.snd : X √ó Y ‚Üí Y) :=
  isOpenMap_snd.isQuotientMap continuous_snd Prod.snd_surjective

@[deprecated (since := "2024-10-22")]
alias quotientMap_snd := isQuotientMap_snd

theorem closure_prod_eq {s : Set X} {t : Set Y} : closure (s √óÀ¢ t) = closure s √óÀ¢ closure t :=
  ext fun ‚ü®a, b‚ü© => by
    simp_rw [mem_prod, mem_closure_iff_nhdsWithin_neBot, nhdsWithin_prod_eq, prod_neBot]

theorem interior_prod_eq (s : Set X) (t : Set Y) : interior (s √óÀ¢ t) = interior s √óÀ¢ interior t :=
  ext fun ‚ü®a, b‚ü© => by simp only [mem_interior_iff_mem_nhds, mem_prod, prod_mem_nhds_iff]

theorem frontier_prod_eq (s : Set X) (t : Set Y) :
    frontier (s √óÀ¢ t) = closure s √óÀ¢ frontier t ‚à™ frontier s √óÀ¢ closure t := by
  simp only [frontier, closure_prod_eq, interior_prod_eq, prod_diff_prod]

@[simp]
theorem frontier_prod_univ_eq (s : Set X) :
    frontier (s √óÀ¢ (univ : Set Y)) = frontier s √óÀ¢ univ := by
  simp [frontier_prod_eq]

@[simp]
theorem frontier_univ_prod_eq (s : Set Y) :
    frontier ((univ : Set X) √óÀ¢ s) = univ √óÀ¢ frontier s := by
  simp [frontier_prod_eq]

theorem map_mem_closure‚ÇÇ {f : X ‚Üí Y ‚Üí Z} {x : X} {y : Y} {s : Set X} {t : Set Y} {u : Set Z}
    (hf : Continuous (uncurry f)) (hx : x ‚àà closure s) (hy : y ‚àà closure t)
    (h : ‚àÄ a ‚àà s, ‚àÄ b ‚àà t, f a b ‚àà u) : f x y ‚àà closure u :=
  have H‚ÇÅ : (x, y) ‚àà closure (s √óÀ¢ t) := by simpa only [closure_prod_eq] using mk_mem_prod hx hy
  have H‚ÇÇ : MapsTo (uncurry f) (s √óÀ¢ t) u := forall_prod_set.2 h
  H‚ÇÇ.closure hf H‚ÇÅ

theorem IsClosed.prod {s‚ÇÅ : Set X} {s‚ÇÇ : Set Y} (h‚ÇÅ : IsClosed s‚ÇÅ) (h‚ÇÇ : IsClosed s‚ÇÇ) :
    IsClosed (s‚ÇÅ √óÀ¢ s‚ÇÇ) :=
  closure_eq_iff_isClosed.mp <| by simp only [h‚ÇÅ.closure_eq, h‚ÇÇ.closure_eq, closure_prod_eq]

/-- The product of two dense sets is a dense set. -/
theorem Dense.prod {s : Set X} {t : Set Y} (hs : Dense s) (ht : Dense t) : Dense (s √óÀ¢ t) :=
  fun x => by
  rw [closure_prod_eq]
  exact ‚ü®hs x.1, ht x.2‚ü©

/-- If `f` and `g` are maps with dense range, then `Prod.map f g` has dense range. -/
theorem DenseRange.prodMap {Œπ : Type*} {Œ∫ : Type*} {f : Œπ ‚Üí Y} {g : Œ∫ ‚Üí Z} (hf : DenseRange f)
    (hg : DenseRange g) : DenseRange (Prod.map f g) := by
  simpa only [DenseRange, prod_range_range_eq] using hf.prod hg

lemma Topology.IsInducing.prodMap {f : X ‚Üí Y} {g : Z ‚Üí W} (hf : IsInducing f) (hg : IsInducing g) :
    IsInducing (Prod.map f g) :=
  isInducing_iff_nhds.2 fun (x, z) => by simp_rw [Prod.map_def, nhds_prod_eq, hf.nhds_eq_comap,
    hg.nhds_eq_comap, prod_comap_comap_eq]

@[deprecated (since := "2024-10-28")] alias Inducing.prodMap := IsInducing.prodMap

@[simp]
lemma Topology.isInducing_const_prod {x : X} {f : Y ‚Üí Z} :
    IsInducing (fun x' => (x, f x')) ‚Üî IsInducing f := by
  simp_rw [isInducing_iff, instTopologicalSpaceProd, induced_inf, induced_compose,
    Function.comp_def, induced_const, top_inf_eq]

@[deprecated (since := "2024-10-28")] alias inducing_const_prod := isInducing_const_prod

@[simp]
lemma Topology.isInducing_prod_const {y : Y} {f : X ‚Üí Z} :
    IsInducing (fun x => (f x, y)) ‚Üî IsInducing f := by
  simp_rw [isInducing_iff, instTopologicalSpaceProd, induced_inf, induced_compose,
    Function.comp_def, induced_const, inf_top_eq]

@[deprecated (since := "2024-10-28")] alias inducing_prod_const := isInducing_prod_const

lemma isInducing_prodMkLeft (y : Y) : IsInducing (fun x : X ‚Ü¶ (x, y)) :=
  .of_comp (.prodMk_left y) continuous_fst .id

lemma isInducing_prodMkRight (x : X) : IsInducing (Prod.mk x : Y ‚Üí X √ó Y) :=
  .of_comp (.prodMk_right x) continuous_snd .id

lemma Topology.IsEmbedding.prodMap {f : X ‚Üí Y} {g : Z ‚Üí W} (hf : IsEmbedding f)
    (hg : IsEmbedding g) : IsEmbedding (Prod.map f g) where
  toIsInducing := hf.isInducing.prodMap hg.isInducing
  injective := hf.injective.prodMap hg.injective

protected theorem IsOpenMap.prodMap {f : X ‚Üí Y} {g : Z ‚Üí W} (hf : IsOpenMap f) (hg : IsOpenMap g) :
    IsOpenMap (Prod.map f g) := by
  rw [isOpenMap_iff_nhds_le]
  rintro ‚ü®a, b‚ü©
  rw [nhds_prod_eq, nhds_prod_eq, ‚Üê Filter.prod_map_map_eq']
  exact Filter.prod_mono (hf.nhds_le a) (hg.nhds_le b)

protected lemma Topology.IsOpenEmbedding.prodMap {f : X ‚Üí Y} {g : Z ‚Üí W} (hf : IsOpenEmbedding f)
    (hg : IsOpenEmbedding g) : IsOpenEmbedding (Prod.map f g) :=
  .of_isEmbedding_isOpenMap (hf.1.prodMap hg.1) (hf.isOpenMap.prodMap hg.isOpenMap)

lemma isEmbedding_graph {f : X ‚Üí Y} (hf : Continuous f) : IsEmbedding fun x => (x, f x) :=
  .of_comp (continuous_id.prodMk hf) continuous_fst .id

@[deprecated (since := "2024-10-26")]
alias embedding_graph := isEmbedding_graph

lemma isEmbedding_prodMkLeft (y : Y) : IsEmbedding (fun x : X ‚Ü¶ (x, y)) :=
  .of_comp (.prodMk_left y) continuous_fst .id

lemma isEmbedding_prodMkRight (x : X) : IsEmbedding (Prod.mk x : Y ‚Üí X √ó Y) :=
  .of_comp (.prodMk_right x) continuous_snd .id

@[deprecated (since := "2025-06-12")] alias isEmbedding_prodMk := isEmbedding_prodMkRight
@[deprecated (since := "2024-10-26")] alias embedding_prod_mk := isEmbedding_prodMkRight

theorem IsOpenQuotientMap.prodMap {f : X ‚Üí Y} {g : Z ‚Üí W} (hf : IsOpenQuotientMap f)
    (hg : IsOpenQuotientMap g) : IsOpenQuotientMap (Prod.map f g) :=
  ‚ü®.prodMap hf.1 hg.1, .prodMap hf.2 hg.2, .prodMap hf.3 hg.3‚ü©

-- Homeomorphisms between the various product: products of two homeomorphisms,
-- as well as commutativity and associativity. See below for the analogous results for sums,
-- as well as distributivity, etc.
namespace Homeomorph

variable {X' Y' : Type*} [TopologicalSpace X'] [TopologicalSpace Y']

/-- Product of two homeomorphisms. -/
def prodCongr (h‚ÇÅ : X ‚âÉ‚Çú X') (h‚ÇÇ : Y ‚âÉ‚Çú Y') : X √ó Y ‚âÉ‚Çú X' √ó Y' where
  toEquiv := h‚ÇÅ.toEquiv.prodCongr h‚ÇÇ.toEquiv

@[simp]
theorem prodCongr_symm (h‚ÇÅ : X ‚âÉ‚Çú X') (h‚ÇÇ : Y ‚âÉ‚Çú Y') :
    (h‚ÇÅ.prodCongr h‚ÇÇ).symm = h‚ÇÅ.symm.prodCongr h‚ÇÇ.symm :=
  rfl

@[simp]
theorem coe_prodCongr (h‚ÇÅ : X ‚âÉ‚Çú X') (h‚ÇÇ : Y ‚âÉ‚Çú Y') : ‚áë(h‚ÇÅ.prodCongr h‚ÇÇ) = Prod.map h‚ÇÅ h‚ÇÇ :=
  rfl

variable (W X Y Z)

/-- `X √ó Y` is homeomorphic to `Y √ó X`. -/
def prodComm : X √ó Y ‚âÉ‚Çú Y √ó X where
  continuous_toFun := continuous_snd.prodMk continuous_fst
  continuous_invFun := continuous_snd.prodMk continuous_fst
  toEquiv := Equiv.prodComm X Y

@[simp]
theorem prodComm_symm : (prodComm X Y).symm = prodComm Y X :=
  rfl

@[simp]
theorem coe_prodComm : ‚áë(prodComm X Y) = Prod.swap :=
  rfl

/-- `(X √ó Y) √ó Z` is homeomorphic to `X √ó (Y √ó Z)`. -/
def prodAssoc : (X √ó Y) √ó Z ‚âÉ‚Çú X √ó Y √ó Z where
  continuous_toFun := continuous_fst.fst.prodMk (continuous_fst.snd.prodMk continuous_snd)
  continuous_invFun := (continuous_fst.prodMk continuous_snd.fst).prodMk continuous_snd.snd
  toEquiv := Equiv.prodAssoc X Y Z

@[simp]
lemma prodAssoc_toEquiv : (prodAssoc X Y Z).toEquiv = Equiv.prodAssoc X Y Z := rfl

/-- Four-way commutativity of `prod`. The name matches `mul_mul_mul_comm`. -/
def prodProdProdComm : (X √ó Y) √ó W √ó Z ‚âÉ‚Çú (X √ó W) √ó Y √ó Z where
  toEquiv := Equiv.prodProdProdComm X Y W Z
  continuous_toFun := by
    unfold Equiv.prodProdProdComm
    dsimp only
    fun_prop
  continuous_invFun := by
    unfold Equiv.prodProdProdComm
    dsimp only
    fun_prop

@[simp]
theorem prodProdProdComm_symm : (prodProdProdComm X Y W Z).symm = prodProdProdComm X W Y Z :=
  rfl

/-- `X √ó {*}` is homeomorphic to `X`. -/
@[simps! -fullyApplied apply]
def prodPUnit : X √ó PUnit ‚âÉ‚Çú X where
  toEquiv := Equiv.prodPUnit X
  continuous_toFun := continuous_fst
  continuous_invFun := .prodMk_left _

/-- `{*} √ó X` is homeomorphic to `X`. -/
def punitProd : PUnit √ó X ‚âÉ‚Çú X :=
  (prodComm _ _).trans (prodPUnit _)

@[simp] theorem coe_punitProd : ‚áë(punitProd X) = Prod.snd := rfl

end Homeomorph

end Prod

section Sum

open Sum

variable [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace W] [TopologicalSpace Z]

theorem continuous_sum_dom {f : X ‚äï Y ‚Üí Z} :
    Continuous f ‚Üî Continuous (f ‚àò Sum.inl) ‚àß Continuous (f ‚àò Sum.inr) :=
  (continuous_sup_dom (t‚ÇÅ := TopologicalSpace.coinduced Sum.inl _)
    (t‚ÇÇ := TopologicalSpace.coinduced Sum.inr _)).trans <|
    continuous_coinduced_dom.and continuous_coinduced_dom

theorem continuous_sumElim {f : X ‚Üí Z} {g : Y ‚Üí Z} :
    Continuous (Sum.elim f g) ‚Üî Continuous f ‚àß Continuous g :=
  continuous_sum_dom

@[deprecated (since := "2025-02-20")] alias continuous_sum_elim := continuous_sumElim

@[continuity, fun_prop]
theorem Continuous.sumElim {f : X ‚Üí Z} {g : Y ‚Üí Z} (hf : Continuous f) (hg : Continuous g) :
    Continuous (Sum.elim f g) :=
  continuous_sumElim.2 ‚ü®hf, hg‚ü©

@[deprecated (since := "2025-02-20")] alias Continuous.sum_elim := Continuous.sumElim

@[continuity, fun_prop]
theorem continuous_isLeft : Continuous (isLeft : X ‚äï Y ‚Üí Bool) :=
  continuous_sum_dom.2 ‚ü®continuous_const, continuous_const‚ü©

@[continuity, fun_prop]
theorem continuous_isRight : Continuous (isRight : X ‚äï Y ‚Üí Bool) :=
  continuous_sum_dom.2 ‚ü®continuous_const, continuous_const‚ü©

@[continuity, fun_prop]
theorem continuous_inl : Continuous (@inl X Y) := ‚ü®fun _ => And.left‚ü©

@[continuity, fun_prop]
theorem continuous_inr : Continuous (@inr X Y) := ‚ü®fun _ => And.right‚ü©

@[fun_prop, continuity]
lemma continuous_sum_swap : Continuous (@Sum.swap X Y) :=
  Continuous.sumElim continuous_inr continuous_inl

theorem isOpen_sum_iff {s : Set (X ‚äï Y)} : IsOpen s ‚Üî IsOpen (inl ‚Åª¬π' s) ‚àß IsOpen (inr ‚Åª¬π' s) :=
  Iff.rfl

theorem isClosed_sum_iff {s : Set (X ‚äï Y)} :
    IsClosed s ‚Üî IsClosed (inl ‚Åª¬π' s) ‚àß IsClosed (inr ‚Åª¬π' s) := by
  simp only [‚Üê isOpen_compl_iff, isOpen_sum_iff, preimage_compl]

theorem isOpenMap_inl : IsOpenMap (@inl X Y) := fun u hu => by
  simpa [isOpen_sum_iff, preimage_image_eq u Sum.inl_injective]

theorem isOpenMap_inr : IsOpenMap (@inr X Y) := fun u hu => by
  simpa [isOpen_sum_iff, preimage_image_eq u Sum.inr_injective]

theorem isClosedMap_inl : IsClosedMap (@inl X Y) := fun u hu ‚Ü¶ by
  simpa [isClosed_sum_iff, preimage_image_eq u Sum.inl_injective]

theorem isClosedMap_inr : IsClosedMap (@inr X Y) := fun u hu ‚Ü¶ by
  simpa [isClosed_sum_iff, preimage_image_eq u Sum.inr_injective]

protected lemma Topology.IsOpenEmbedding.inl : IsOpenEmbedding (@inl X Y) :=
  .of_continuous_injective_isOpenMap continuous_inl inl_injective isOpenMap_inl

@[deprecated (since := "2024-10-30")] alias isOpenEmbedding_inl := IsOpenEmbedding.inl

protected lemma Topology.IsOpenEmbedding.inr : IsOpenEmbedding (@inr X Y) :=
  .of_continuous_injective_isOpenMap continuous_inr inr_injective isOpenMap_inr

@[deprecated (since := "2024-10-30")] alias isOpenEmbedding_inr := IsOpenEmbedding.inr

protected lemma Topology.IsEmbedding.inl : IsEmbedding (@inl X Y) := IsOpenEmbedding.inl.1
protected lemma Topology.IsEmbedding.inr : IsEmbedding (@inr X Y) := IsOpenEmbedding.inr.1

@[deprecated (since := "2024-10-26")]
alias embedding_inr := IsEmbedding.inr

lemma isOpen_range_inl : IsOpen (range (inl : X ‚Üí X ‚äï Y)) := IsOpenEmbedding.inl.2
lemma isOpen_range_inr : IsOpen (range (inr : Y ‚Üí X ‚äï Y)) := IsOpenEmbedding.inr.2

theorem isClosed_range_inl : IsClosed (range (inl : X ‚Üí X ‚äï Y)) := by
  rw [‚Üê isOpen_compl_iff, compl_range_inl]
  exact isOpen_range_inr

theorem isClosed_range_inr : IsClosed (range (inr : Y ‚Üí X ‚äï Y)) := by
  rw [‚Üê isOpen_compl_iff, compl_range_inr]
  exact isOpen_range_inl

theorem Topology.IsClosedEmbedding.inl : IsClosedEmbedding (inl : X ‚Üí X ‚äï Y) :=
  ‚ü®.inl, isClosed_range_inl‚ü©

@[deprecated (since := "2024-10-30")] alias isClosedEmbedding_inl := IsClosedEmbedding.inl

theorem Topology.IsClosedEmbedding.inr : IsClosedEmbedding (inr : Y ‚Üí X ‚äï Y) :=
  ‚ü®.inr, isClosed_range_inr‚ü©

@[deprecated (since := "2024-10-30")] alias isClosedEmbedding_inr := IsClosedEmbedding.inr

theorem nhds_inl (x : X) : ùìù (inl x : X ‚äï Y) = map inl (ùìù x) :=
  (IsOpenEmbedding.inl.map_nhds_eq _).symm

theorem nhds_inr (y : Y) : ùìù (inr y : X ‚äï Y) = map inr (ùìù y) :=
  (IsOpenEmbedding.inr.map_nhds_eq _).symm

@[simp]
theorem continuous_sumMap {f : X ‚Üí Y} {g : Z ‚Üí W} :
    Continuous (Sum.map f g) ‚Üî Continuous f ‚àß Continuous g :=
  continuous_sumElim.trans <|
    IsEmbedding.inl.continuous_iff.symm.and IsEmbedding.inr.continuous_iff.symm

@[deprecated (since := "2025-02-21")] alias continuous_sum_map := continuous_sumMap

@[continuity, fun_prop]
theorem Continuous.sumMap {f : X ‚Üí Y} {g : Z ‚Üí W} (hf : Continuous f) (hg : Continuous g) :
    Continuous (Sum.map f g) :=
  continuous_sumMap.2 ‚ü®hf, hg‚ü©

@[deprecated (since := "2025-02-21")] alias Continuous.sum_map := Continuous.sumMap

theorem isOpenMap_sum {f : X ‚äï Y ‚Üí Z} :
    IsOpenMap f ‚Üî (IsOpenMap fun a => f (inl a)) ‚àß IsOpenMap fun b => f (inr b) := by
  simp only [isOpenMap_iff_nhds_le, Sum.forall, nhds_inl, nhds_inr, Filter.map_map, comp_def]

theorem IsOpenMap.sumMap {f : X ‚Üí Y} {g : Z ‚Üí W} (hf : IsOpenMap f) (hg : IsOpenMap g) :
    IsOpenMap (Sum.map f g) :=
  isOpenMap_sum.2 ‚ü®isOpenMap_inl.comp hf, isOpenMap_inr.comp hg‚ü©

@[simp]
theorem isOpenMap_sumElim {f : X ‚Üí Z} {g : Y ‚Üí Z} :
    IsOpenMap (Sum.elim f g) ‚Üî IsOpenMap f ‚àß IsOpenMap g := by
  simp only [isOpenMap_sum, elim_inl, elim_inr]

@[deprecated (since := "2025-02-20")] alias isOpenMap_sum_elim := isOpenMap_sumElim

theorem IsOpenMap.sumElim {f : X ‚Üí Z} {g : Y ‚Üí Z} (hf : IsOpenMap f) (hg : IsOpenMap g) :
    IsOpenMap (Sum.elim f g) :=
  isOpenMap_sumElim.2 ‚ü®hf, hg‚ü©

@[deprecated (since := "2025-02-20")] alias IsOpenMap.sum_elim := IsOpenMap.sumElim

lemma IsOpenEmbedding.sumElim {f : X ‚Üí Z} {g : Y ‚Üí Z}
    (hf : IsOpenEmbedding f) (hg : IsOpenEmbedding g) (h : Injective (Sum.elim f g)) :
    IsOpenEmbedding (Sum.elim f g) := by
  rw [isOpenEmbedding_iff_continuous_injective_isOpenMap] at hf hg ‚ä¢
  exact ‚ü®hf.1.sumElim hg.1, h, hf.2.2.sumElim hg.2.2‚ü©

theorem isClosedMap_sum {f : X ‚äï Y ‚Üí Z} :
    IsClosedMap f ‚Üî (IsClosedMap fun a => f (.inl a)) ‚àß IsClosedMap fun b => f (.inr b) := by
  constructor
  ¬∑ intro h
    exact ‚ü®h.comp IsClosedEmbedding.inl.isClosedMap, h.comp IsClosedEmbedding.inr.isClosedMap‚ü©
  ¬∑ rintro h Z hZ
    rw [isClosed_sum_iff] at hZ
    convert (h.1 _ hZ.1).union (h.2 _ hZ.2)
    ext
    simp only [mem_image, Sum.exists, mem_union, mem_preimage]

theorem IsClosedMap.sumMap {f : X ‚Üí Y} {g : Z ‚Üí W} (hf : IsClosedMap f) (hg : IsClosedMap g) :
    IsClosedMap (Sum.map f g) :=
  isClosedMap_sum.2 ‚ü®isClosedMap_inl.comp hf, isClosedMap_inr.comp hg‚ü©

@[simp]
theorem isClosedMap_sumElim {f : X ‚Üí Z} {g : Y ‚Üí Z} :
    IsClosedMap (Sum.elim f g) ‚Üî IsClosedMap f ‚àß IsClosedMap g := by
  simp only [isClosedMap_sum, Sum.elim_inl, Sum.elim_inr]

theorem IsClosedMap.sumElim {f : X ‚Üí Z} {g : Y ‚Üí Z} (hf : IsClosedMap f) (hg : IsClosedMap g) :
    IsClosedMap (Sum.elim f g) :=
  isClosedMap_sumElim.2 ‚ü®hf, hg‚ü©

lemma IsClosedEmbedding.sumElim {f : X ‚Üí Z} {g : Y ‚Üí Z}
    (hf : IsClosedEmbedding f) (hg : IsClosedEmbedding g) (h : Injective (Sum.elim f g)) :
    IsClosedEmbedding (Sum.elim f g) := by
  rw [IsClosedEmbedding.isClosedEmbedding_iff_continuous_injective_isClosedMap] at hf hg ‚ä¢
  exact ‚ü®hf.1.sumElim hg.1, h, hf.2.2.sumElim hg.2.2‚ü©

-- Homeomorphisms between the various constructions: sums of two homeomorphisms,
-- as well as commutativity, associativity and distributivity with products.
namespace Homeomorph

variable {X' Y' : Type*} [TopologicalSpace X'] [TopologicalSpace Y']

/-- Sum of two homeomorphisms. -/
def sumCongr (h‚ÇÅ : X ‚âÉ‚Çú X') (h‚ÇÇ : Y ‚âÉ‚Çú Y') : X ‚äï Y ‚âÉ‚Çú X' ‚äï Y' where
  continuous_toFun := h‚ÇÅ.continuous.sumMap h‚ÇÇ.continuous
  continuous_invFun := h‚ÇÅ.symm.continuous.sumMap h‚ÇÇ.symm.continuous
  toEquiv := h‚ÇÅ.toEquiv.sumCongr h‚ÇÇ.toEquiv

@[simp]
lemma sumCongr_symm (h‚ÇÅ : X ‚âÉ‚Çú X') (h‚ÇÇ : Y ‚âÉ‚Çú Y') :
  (sumCongr h‚ÇÅ h‚ÇÇ).symm = sumCongr h‚ÇÅ.symm h‚ÇÇ.symm := rfl

@[simp]
theorem sumCongr_refl : sumCongr (.refl X) (.refl Y) = .refl (X ‚äï Y) := by
  ext i
  cases i <;> rfl

@[simp]
theorem sumCongr_trans {X'' Y'' : Type*} [TopologicalSpace X''] [TopologicalSpace Y'']
    (h‚ÇÅ : X ‚âÉ‚Çú X') (h‚ÇÇ : Y ‚âÉ‚Çú Y') (h‚ÇÉ : X' ‚âÉ‚Çú X'') (h‚ÇÑ : Y' ‚âÉ‚Çú Y'') :
    (sumCongr h‚ÇÅ h‚ÇÇ).trans (sumCongr h‚ÇÉ h‚ÇÑ) = sumCongr (h‚ÇÅ.trans h‚ÇÉ) (h‚ÇÇ.trans h‚ÇÑ) := by
  ext i
  cases i <;> rfl

variable (W X Y Z)

/-- `X ‚äï Y` is homeomorphic to `Y ‚äï X`. -/
def sumComm : X ‚äï Y ‚âÉ‚Çú Y ‚äï X where
  toEquiv := Equiv.sumComm X Y
  continuous_toFun := continuous_sum_swap
  continuous_invFun := continuous_sum_swap

@[simp]
theorem sumComm_symm : (sumComm X Y).symm = sumComm Y X :=
  rfl

@[simp]
theorem coe_sumComm : ‚áë(sumComm X Y) = Sum.swap :=
  rfl

@[continuity, fun_prop]
lemma continuous_sumAssoc : Continuous (Equiv.sumAssoc X Y Z) :=
  Continuous.sumElim (by fun_prop) (by fun_prop)

@[continuity, fun_prop]
lemma continuous_sumAssoc_symm : Continuous (Equiv.sumAssoc X Y Z).symm :=
  Continuous.sumElim (by fun_prop) (by fun_prop)

/-- `(X ‚äï Y) ‚äï Z` is homeomorphic to `X ‚äï (Y ‚äï Z)`. -/
def sumAssoc : (X ‚äï Y) ‚äï Z ‚âÉ‚Çú X ‚äï Y ‚äï Z where
  toEquiv := Equiv.sumAssoc X Y Z
  continuous_toFun := continuous_sumAssoc X Y Z
  continuous_invFun := continuous_sumAssoc_symm X Y Z

@[simp]
lemma sumAssoc_toEquiv : (sumAssoc X Y Z).toEquiv = Equiv.sumAssoc X Y Z := rfl

/-- Four-way commutativity of the disjoint union. The name matches `add_add_add_comm`. -/
def sumSumSumComm : (X ‚äï Y) ‚äï W ‚äï Z ‚âÉ‚Çú (X ‚äï W) ‚äï Y ‚äï Z where
  toEquiv := Equiv.sumSumSumComm X Y W Z
  continuous_toFun := by
    unfold Equiv.sumSumSumComm
    dsimp only
    have : Continuous (Sum.map (Sum.map (@id X) ‚áë(Equiv.sumComm Y W)) (@id Z)) := by continuity
    fun_prop
  continuous_invFun := by
    unfold Equiv.sumSumSumComm
    dsimp only
    have : Continuous (Sum.map (Sum.map (@id X) (Equiv.sumComm Y W).symm) (@id Z)) := by continuity
    fun_prop

@[simp]
lemma sumSumSumComm_toEquiv : (sumSumSumComm W X Y Z).toEquiv = (Equiv.sumSumSumComm W X Y Z) := rfl

@[simp]
lemma sumSumSumComm_symm : (sumSumSumComm X Y W Z).symm = (sumSumSumComm X W Y Z) := rfl

/-- The sum of `X` with any empty topological space is homeomorphic to `X`. -/
@[simps! -fullyApplied apply]
def sumEmpty [IsEmpty Y] : X ‚äï Y ‚âÉ‚Çú X where
  toEquiv := Equiv.sumEmpty X Y
  continuous_toFun := Continuous.sumElim continuous_id (by fun_prop)
  continuous_invFun := continuous_inl

/-- The sum of `X` with any empty topological space is homeomorphic to `X`. -/
def emptySum [IsEmpty Y] : Y ‚äï X ‚âÉ‚Çú X := (sumComm Y X).trans (sumEmpty X Y)

@[simp] theorem coe_emptySum [IsEmpty Y] : (emptySum X Y).toEquiv = Equiv.emptySum Y X := rfl

variable {W X Y Z}

/-- `(X ‚äï Y) √ó Z` is homeomorphic to `X √ó Z ‚äï Y √ó Z`. -/
@[simps!]
def sumProdDistrib : (X ‚äï Y) √ó Z ‚âÉ‚Çú (X √ó Z) ‚äï (Y √ó Z) :=
  Homeomorph.symm <|
    (Equiv.sumProdDistrib X Y Z).symm.toHomeomorphOfContinuousOpen
        ((continuous_inl.prodMap continuous_id).sumElim
          (continuous_inr.prodMap continuous_id)) <|
      (isOpenMap_inl.prodMap IsOpenMap.id).sumElim (isOpenMap_inr.prodMap IsOpenMap.id)

/-- `X √ó (Y ‚äï Z)` is homeomorphic to `X √ó Y ‚äï X √ó Z`. -/
def prodSumDistrib : X √ó (Y ‚äï Z) ‚âÉ‚Çú (X √ó Y) ‚äï (X √ó Z) :=
  (prodComm _ _).trans <| sumProdDistrib.trans <| sumCongr (prodComm _ _) (prodComm _ _)

end Homeomorph

end Sum
