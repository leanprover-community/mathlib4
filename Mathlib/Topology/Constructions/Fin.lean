/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Patrick Massot
-/
import Mathlib.Data.Fin.VecNotation
import Mathlib.Topology.Constructions

/-!
# Pi types and finiteness
-/

open scoped Topology

universe u v u' v'
variable {X : Type u} {Y : Type v} {Z W Îµ Î¶ : Type*} [TopologicalSpace X]

section Fin

variable {n : â„•} {Ï€ : Fin (n + 1) â†’ Type*} [âˆ€ i, TopologicalSpace (Ï€ i)]

theorem Filter.Tendsto.finCons
    {f : Y â†’ Ï€ 0} {g : Y â†’ âˆ€ j : Fin n, Ï€ j.succ} {l : Filter Y} {x : Ï€ 0} {y : âˆ€ j, Ï€ (Fin.succ j)}
    (hf : Tendsto f l (ğ“ x)) (hg : Tendsto g l (ğ“ y)) :
    Tendsto (fun a => Fin.cons (f a) (g a)) l (ğ“ <| Fin.cons x y) :=
  tendsto_pi_nhds.2 fun j => Fin.cases (by simpa) (by simpa using tendsto_pi_nhds.1 hg) j

theorem ContinuousAt.finCons {f : X â†’ Ï€ 0} {g : X â†’ âˆ€ j : Fin n, Ï€ (Fin.succ j)} {x : X}
    (hf : ContinuousAt f x) (hg : ContinuousAt g x) :
    ContinuousAt (fun a => Fin.cons (f a) (g a)) x :=
  hf.tendsto.finCons hg

theorem Continuous.finCons {f : X â†’ Ï€ 0} {g : X â†’ âˆ€ j : Fin n, Ï€ (Fin.succ j)}
    (hf : Continuous f) (hg : Continuous g) : Continuous fun a => Fin.cons (f a) (g a) :=
  continuous_iff_continuousAt.2 fun _ => hf.continuousAt.finCons hg.continuousAt

variable [TopologicalSpace Z]

theorem Filter.Tendsto.matrixVecCons
    {f : Y â†’ Z} {g : Y â†’ Fin n â†’ Z} {l : Filter Y} {x : Z} {y : Fin n â†’ Z}
    (hf : Tendsto f l (ğ“ x)) (hg : Tendsto g l (ğ“ y)) :
    Tendsto (fun a => Matrix.vecCons (f a) (g a)) l (ğ“ <| Matrix.vecCons x y) :=
  hf.finCons hg

theorem ContinuousAt.matrixVecCons
    {f : X â†’ Z} {g : X â†’ Fin n â†’ Z} {x : X} (hf : ContinuousAt f x) (hg : ContinuousAt g x) :
    ContinuousAt (fun a => Matrix.vecCons (f a) (g a)) x :=
  hf.finCons hg

theorem Continuous.matrixVecCons
    {f : X â†’ Z} {g : X â†’ Fin n â†’ Z} (hf : Continuous f) (hg : Continuous g) :
    Continuous fun a => Matrix.vecCons (f a) (g a) :=
  hf.finCons hg

theorem Filter.Tendsto.finSnoc
    {f : Y â†’ âˆ€ j : Fin n, Ï€ j.castSucc} {g : Y â†’ Ï€ (Fin.last _)}
    {l : Filter Y} {x : âˆ€ j, Ï€ (Fin.castSucc j)} {y : Ï€ (Fin.last _)}
    (hf : Tendsto f l (ğ“ x)) (hg : Tendsto g l (ğ“ y)) :
    Tendsto (fun a => Fin.snoc (f a) (g a)) l (ğ“ <| Fin.snoc x y) :=
  tendsto_pi_nhds.2 fun j => Fin.lastCases (by simpa) (by simpa using tendsto_pi_nhds.1 hf) j

theorem ContinuousAt.finSnoc {f : X â†’ âˆ€ j : Fin n, Ï€ j.castSucc} {g : X â†’ Ï€ (Fin.last _)} {x : X}
    (hf : ContinuousAt f x) (hg : ContinuousAt g x) :
    ContinuousAt (fun a => Fin.snoc (f a) (g a)) x :=
  hf.tendsto.finSnoc hg

theorem Continuous.finSnoc {f : X â†’ âˆ€ j : Fin n, Ï€ j.castSucc} {g : X â†’ Ï€ (Fin.last _)}
    (hf : Continuous f) (hg : Continuous g) : Continuous fun a => Fin.snoc (f a) (g a) :=
  continuous_iff_continuousAt.2 fun _ => hf.continuousAt.finSnoc hg.continuousAt

theorem Filter.Tendsto.finInsertNth
    (i : Fin (n + 1)) {f : Y â†’ Ï€ i} {g : Y â†’ âˆ€ j : Fin n, Ï€ (i.succAbove j)} {l : Filter Y}
    {x : Ï€ i} {y : âˆ€ j, Ï€ (i.succAbove j)} (hf : Tendsto f l (ğ“ x)) (hg : Tendsto g l (ğ“ y)) :
    Tendsto (fun a => i.insertNth (f a) (g a)) l (ğ“ <| i.insertNth x y) :=
  tendsto_pi_nhds.2 fun j => Fin.succAboveCases i (by simpa) (by simpa using tendsto_pi_nhds.1 hg) j

@[deprecated (since := "2025-01-02")]
alias Filter.Tendsto.fin_insertNth := Filter.Tendsto.finInsertNth

theorem ContinuousAt.finInsertNth
    (i : Fin (n + 1)) {f : X â†’ Ï€ i} {g : X â†’ âˆ€ j : Fin n, Ï€ (i.succAbove j)} {x : X}
    (hf : ContinuousAt f x) (hg : ContinuousAt g x) :
    ContinuousAt (fun a => i.insertNth (f a) (g a)) x :=
  hf.tendsto.finInsertNth i hg

@[deprecated (since := "2025-01-02")]
alias ContinuousAt.fin_insertNth := ContinuousAt.finInsertNth

theorem Continuous.finInsertNth
    (i : Fin (n + 1)) {f : X â†’ Ï€ i} {g : X â†’ âˆ€ j : Fin n, Ï€ (i.succAbove j)}
    (hf : Continuous f) (hg : Continuous g) : Continuous fun a => i.insertNth (f a) (g a) :=
  continuous_iff_continuousAt.2 fun _ => hf.continuousAt.finInsertNth i hg.continuousAt

@[deprecated (since := "2025-01-02")]
alias Continuous.fin_insertNth := Continuous.finInsertNth

end Fin
