/-
Copyright (c) 2024 Daniel Weber. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Daniel Weber
-/
import Mathlib.Topology.Separation

/-!
# Derived set

This file defines the derived set of a set, the set of all `AccPt`s of its principal filter,
and proves some properties of it.

-/

open Filter Topology

variable {X : Type*} [TopologicalSpace X]

theorem AccPt.map {Î² : Type*} [TopologicalSpace Î²] {F : Filter X} {x : X}
    (h : AccPt x F) {f : X â†’ Î²} (hf1 : ContinuousAt f x) (hf2 : Function.Injective f) :
    AccPt (f x) (map f F) := by
  apply map_neBot (m := f) (hf := h) |>.mono
  rw [Filter.map_inf hf2]
  gcongr
  apply tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within _ hf1.continuousWithinAt
  simpa [hf2.eq_iff] using eventually_mem_nhdsWithin

/--
The derived set of a set is the set of all accumulation points of it.
-/
def derivedSet (A : Set X) : Set X := {x | AccPt x (ğ“Ÿ A)}

@[simp]
lemma mem_derivedSet {A : Set X} {x : X} : x âˆˆ derivedSet A â†” AccPt x (ğ“Ÿ A) := Iff.rfl

lemma derivedSet_union (A B : Set X) : derivedSet (A âˆª B) = derivedSet A âˆª derivedSet B := by
  ext x
  simp [derivedSet, â† sup_principal, accPt_sup]

lemma derivedSet_mono (A B : Set X) (h : A âŠ† B) : derivedSet A âŠ† derivedSet B :=
  fun _ hx â†¦ hx.mono <| le_principal_iff.mpr <| mem_principal.mpr h

theorem image_derivedSet_subset {Î² : Type*} [TopologicalSpace Î²] {A : Set X} {f : X â†’ Î²}
    (hf1 : Continuous f) (hf2 : Function.Injective f) :
    f '' derivedSet A âŠ† derivedSet (f '' A) := by
  intro x hx
  simp [Set.mem_image, mem_derivedSet] at hx
  obtain âŸ¨y, hy1, rflâŸ© := hx
  convert hy1.map hf1.continuousAt hf2
  simp

lemma isClosed_iff_derivedSet_subset (A : Set X) : IsClosed A â†” derivedSet A âŠ† A where
  mp h := by
    rw [isClosed_iff_clusterPt] at h
    intro x hx
    apply h
    apply hx.clusterPt
  mpr h := by
    rw [isClosed_iff_clusterPt]
    intro a ha
    by_contra! nh
    have : A = A \ {a} := by simp [nh]
    rw [this, â† acc_principal_iff_cluster] at ha
    exact nh (h ha)

@[simp]
lemma isClosed_derivedSet [T1Space X] (A : Set X) : IsClosed (derivedSet A) := by
  rw [isClosed_iff_derivedSet_subset]
  intro x hx
  rw [derivedSet, Set.mem_setOf, accPt_iff_frequently, frequently_nhds_iff] at hx âŠ¢
  intro U hu1 hu2
  obtain âŸ¨y, hy, âŸ¨hy1, hy2âŸ©âŸ© := hx U hu1 hu2
  rw [derivedSet, Set.mem_setOf, accPt_iff_frequently, frequently_nhds_iff] at hy2
  obtain âŸ¨z, hzâŸ© := hy2 (U \ {x}) (IsOpen.sdiff hu1 isClosed_singleton) (by simp [hy1, hy])
  simp at hz
  use z
  simp [hz]

lemma IsPreconnected.subset_derivedSet_self [T1Space X] {U : Set X} (hu : U.Nontrivial)
    (h : IsPreconnected U) : U âŠ† derivedSet U := by
  intro x hx
  rw [isPreconnected_closed_iff] at h
  replace h := h {x} (closure (U \ {x})) isClosed_singleton isClosed_closure (by
    trans {x} âˆª (U \ {x})
    Â· simp
    apply Set.union_subset_union_right
    exact subset_closure
  ) (Set.inter_singleton_nonempty.mpr hx) (by
    obtain âŸ¨y, hyâŸ© := Set.Nontrivial.exists_ne hu x
    use y
    simp only [Set.mem_inter_iff, hy, true_and]
    apply subset_closure
    simp [hy]
  )
  apply Set.Nonempty.right at h
  rw [Set.singleton_inter_nonempty, mem_closure_iff_clusterPt, â† acc_principal_iff_cluster] at h
  exact h


lemma IsPreconnected.inter_derivedSet_nonempty [T1Space X] {U : Set X} (hs : IsPreconnected U)
    (a b : Set X) (h : U âŠ† a âˆª b) (ha : (U âˆ© derivedSet a).Nonempty)
    (hb : (U âˆ© derivedSet b).Nonempty) : (U âˆ© (derivedSet a âˆ© derivedSet b)).Nonempty := by
  by_cases hu : U.Nontrivial
  Â· apply isPreconnected_closed_iff.mp hs
    Â· simp
    Â· simp
    Â· trans derivedSet U
      Â· apply hs.subset_derivedSet_self hu
      Â· rw [â† derivedSet_union]
        exact derivedSet_mono _ _ h
    Â· exact ha
    Â· exact hb
  Â· obtain âŸ¨x, hxâŸ© := ha.left.exists_eq_singleton_or_nontrivial.resolve_right hu
    simp_all
