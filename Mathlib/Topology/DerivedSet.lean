/-
Copyright (c) 2024 Daniel Weber. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Daniel Weber
-/
import Mathlib.Topology.Separation

open Filter Topology

variable {X : Type*} [TopologicalSpace X]

def derivedSet (A : Set X) : Set X := {x | AccPt x (ğ“Ÿ A)}

lemma derivedSet_union (A B : Set X) : derivedSet (A âˆª B) = derivedSet A âˆª derivedSet B := by
  ext x
  simp only [derivedSet, â† sup_principal, accPt_sup]

lemma derivedSet_mono (A B : Set X) (h : A âŠ† B) : derivedSet A âŠ† derivedSet B := by
  intro x hx
  apply hx.mono
  simp [h]

lemma isClosed_iff_derivedSet_subset (A : Set X) : IsClosed A â†” derivedSet A âŠ† A where
  mp h := by
    rw [isClosed_iff_clusterPt] at h
    intro x hx
    apply h
    apply hx.clusterPt
  mpr h := by
    rw [isClosed_iff_clusterPt]
    intro a ha
    by_contra! nh
    have : A = A \ {a} := by simp [nh]
    rw [this, â† acc_principal_iff_cluster] at ha
    exact nh (h ha)

theorem frequently_nhds_iff {x : X} {p : X â†’ Prop} :
    (âˆƒá¶  (x : X) in ğ“ x, p x) â†” âˆ€ U, IsOpen U â†’ x âˆˆ U â†’ âˆƒ x âˆˆ U, p x := by
  simp only [frequently_iff, mem_nhds_iff, forall_exists_index, and_imp]
  constructor
  Â· intro a b c d
    exact a b subset_rfl c d
  Â· intro a _ c d e f
    obtain âŸ¨v, hv, hv2âŸ© := a c e f
    exact âŸ¨v, d hv, hv2âŸ©

@[simp]
lemma isClosed_derivedSet [T1Space X] (A : Set X) : IsClosed (derivedSet A) := by
  rw [isClosed_iff_derivedSet_subset]
  intro x hx
  rw [derivedSet, Set.mem_setOf, accPt_iff_frequently, frequently_nhds_iff] at hx âŠ¢
  intro U hu1 hu2
  obtain âŸ¨y, hy, âŸ¨hy1, hy2âŸ©âŸ© := hx U hu1 hu2
  rw [derivedSet, Set.mem_setOf, accPt_iff_frequently, frequently_nhds_iff] at hy2
  obtain âŸ¨z, hzâŸ© := hy2 (U \ {x}) (IsOpen.sdiff hu1 isClosed_singleton) (by simp [hy1, hy])
  simp at hz
  use z
  simp [hz]

lemma connected_subset_derivedSet [T1Space X] (U : Set X) (hu : U.Nontrivial)
    (h : IsPreconnected U) : U âŠ† derivedSet U := by
  intro x hx
  rw [isPreconnected_closed_iff] at h
  replace h := h {x} (closure (U \ {x})) isClosed_singleton isClosed_closure (by
    trans {x} âˆª (U \ {x})
    Â· simp
    apply Set.union_subset_union_right
    exact subset_closure
  ) (Set.inter_singleton_nonempty.mpr hx) (by
    obtain âŸ¨y, hyâŸ© := Set.Nontrivial.exists_ne hu x
    use y
    simp only [Set.mem_inter_iff, hy, true_and]
    apply subset_closure
    simp [hy]
  )
  apply Set.Nonempty.right at h
  rw [Set.singleton_inter_nonempty, mem_closure_iff_clusterPt, â† acc_principal_iff_cluster] at h
  exact h


lemma preconnected_shared_accPts [T1Space X] (U : Set X) (hs : IsPreconnected U) (a b : Set X)
    (h : U âŠ† a âˆª b) (ha : (U âˆ© derivedSet a).Nonempty) (hb : (U âˆ© derivedSet b).Nonempty) :
    (U âˆ© (derivedSet a âˆ© derivedSet b)).Nonempty := by
  by_cases hu : U.Nontrivial
  Â· apply isPreconnected_closed_iff.mp hs
    Â· simp
    Â· simp
    Â· trans derivedSet U
      Â· apply connected_subset_derivedSet U hu hs
      Â· rw [â† derivedSet_union]
        exact derivedSet_mono _ _ h
    Â· exact ha
    Â· exact hb
  Â· obtain âŸ¨x, hxâŸ© := ha.left.exists_eq_singleton_or_nontrivial.resolve_right hu
    simp_all
