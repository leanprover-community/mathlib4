/-
Copyright (c) 2022 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn, Patrick Massot
-/
import Mathlib.Algebra.GroupWithZero.Indicator
import Mathlib.Algebra.Module.Basic
import Mathlib.Topology.Separation

#align_import topology.support from "leanprover-community/mathlib"@"d90e4e186f1d18e375dcd4e5b5f6364b01cb3e46"

/-!
# The topological support of a function

In this file we define the topological support of a function `f`, `tsupport f`, as the closure of
the support of `f`.

Furthermore, we say that `f` has compact support if the topological support of `f` is compact.

## Main definitions

* `mulTSupport` & `tsupport`
* `HasCompactMulSupport` & `HasCompactSupport`

## Implementation Notes

* We write all lemmas for multiplicative functions, and use `@[to_additive]` to get the more common
  additive versions.
* We do not put the definitions in the `Function` namespace, following many other topological
  definitions that are in the root namespace (compare `Embedding` vs `Function.Embedding`).
-/


open Function Set Filter Topology

variable {X Œ± Œ±' Œ≤ Œ≥ Œ¥ M E R : Type*}

section One

variable [One Œ±] [TopologicalSpace X]

/-- The topological support of a function is the closure of its support, i.e. the closure of the
  set of all elements where the function is not equal to 1. -/
@[to_additive " The topological support of a function is the closure of its support. i.e. the
closure of the set of all elements where the function is nonzero. "]
def mulTSupport (f : X ‚Üí Œ±) : Set X := closure (mulSupport f)
#align mul_tsupport mulTSupport
#align tsupport tsupport

@[to_additive]
theorem subset_mulTSupport (f : X ‚Üí Œ±) : mulSupport f ‚äÜ mulTSupport f :=
  subset_closure
#align subset_mul_tsupport subset_mulTSupport
#align subset_tsupport subset_tsupport

@[to_additive]
theorem isClosed_mulTSupport (f : X ‚Üí Œ±) : IsClosed (mulTSupport f) :=
  isClosed_closure
#align is_closed_mul_tsupport isClosed_mulTSupport
#align is_closed_tsupport isClosed_tsupport

@[to_additive]
theorem mulTSupport_eq_empty_iff {f : X ‚Üí Œ±} : mulTSupport f = ‚àÖ ‚Üî f = 1 := by
  rw [mulTSupport, closure_empty_iff, mulSupport_eq_empty_iff]
#align mul_tsupport_eq_empty_iff mulTSupport_eq_empty_iff
#align tsupport_eq_empty_iff tsupport_eq_empty_iff

@[to_additive]
theorem image_eq_one_of_nmem_mulTSupport {f : X ‚Üí Œ±} {x : X} (hx : x ‚àâ mulTSupport f) : f x = 1 :=
  mulSupport_subset_iff'.mp (subset_mulTSupport f) x hx
#align image_eq_one_of_nmem_mul_tsupport image_eq_one_of_nmem_mulTSupport
#align image_eq_zero_of_nmem_tsupport image_eq_zero_of_nmem_tsupport

@[to_additive]
theorem range_subset_insert_image_mulTSupport (f : X ‚Üí Œ±) :
    range f ‚äÜ insert 1 (f '' mulTSupport f) :=
  (range_subset_insert_image_mulSupport f).trans <|
    insert_subset_insert <| image_subset _ subset_closure
#align range_subset_insert_image_mul_tsupport range_subset_insert_image_mulTSupport
#align range_subset_insert_image_tsupport range_subset_insert_image_tsupport

@[to_additive]
theorem range_eq_image_mulTSupport_or (f : X ‚Üí Œ±) :
    range f = f '' mulTSupport f ‚à® range f = insert 1 (f '' mulTSupport f) :=
  (wcovBy_insert _ _).eq_or_eq (image_subset_range _ _) (range_subset_insert_image_mulTSupport f)
#align range_eq_image_mul_tsupport_or range_eq_image_mulTSupport_or
#align range_eq_image_tsupport_or range_eq_image_tsupport_or

theorem tsupport_mul_subset_left {Œ± : Type*} [MulZeroClass Œ±] {f g : X ‚Üí Œ±} :
    (tsupport fun x => f x * g x) ‚äÜ tsupport f :=
  closure_mono (support_mul_subset_left _ _)
#align tsupport_mul_subset_left tsupport_mul_subset_left

theorem tsupport_mul_subset_right {Œ± : Type*} [MulZeroClass Œ±] {f g : X ‚Üí Œ±} :
    (tsupport fun x => f x * g x) ‚äÜ tsupport g :=
  closure_mono (support_mul_subset_right _ _)
#align tsupport_mul_subset_right tsupport_mul_subset_right

end One

theorem tsupport_smul_subset_left {M Œ±} [TopologicalSpace X] [Zero M] [Zero Œ±] [SMulWithZero M Œ±]
    (f : X ‚Üí M) (g : X ‚Üí Œ±) : (tsupport fun x => f x ‚Ä¢ g x) ‚äÜ tsupport f :=
  closure_mono <| support_smul_subset_left f g
#align tsupport_smul_subset_left tsupport_smul_subset_left

theorem tsupport_smul_subset_right {M Œ±} [TopologicalSpace X] [Zero Œ±] [SMulZeroClass M Œ±]
    (f : X ‚Üí M) (g : X ‚Üí Œ±) : (tsupport fun x => f x ‚Ä¢ g x) ‚äÜ tsupport g :=
  closure_mono <| support_smul_subset_right f g

@[to_additive]
theorem mulTSupport_mul [TopologicalSpace X] [Monoid Œ±] {f g : X ‚Üí Œ±} :
    (mulTSupport fun x ‚Ü¶ f x * g x) ‚äÜ mulTSupport f ‚à™ mulTSupport g :=
  closure_minimal
    ((mulSupport_mul f g).trans (union_subset_union (subset_mulTSupport _) (subset_mulTSupport _)))
    (isClosed_closure.union isClosed_closure)

section

variable [TopologicalSpace Œ±] [TopologicalSpace Œ±']
variable [One Œ≤] [One Œ≥] [One Œ¥]
variable {g : Œ≤ ‚Üí Œ≥} {f : Œ± ‚Üí Œ≤} {f‚ÇÇ : Œ± ‚Üí Œ≥} {m : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥} {x : Œ±}

@[to_additive]
theorem not_mem_mulTSupport_iff_eventuallyEq : x ‚àâ mulTSupport f ‚Üî f =·∂†[ùìù x] 1 := by
  simp_rw [mulTSupport, mem_closure_iff_nhds, not_forall, not_nonempty_iff_eq_empty, exists_prop,
    ‚Üê disjoint_iff_inter_eq_empty, disjoint_mulSupport_iff, eventuallyEq_iff_exists_mem]
#align not_mem_mul_tsupport_iff_eventually_eq not_mem_mulTSupport_iff_eventuallyEq
#align not_mem_tsupport_iff_eventually_eq not_mem_tsupport_iff_eventuallyEq

@[to_additive]
theorem continuous_of_mulTSupport [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : ‚àÄ x ‚àà mulTSupport f, ContinuousAt f x) : Continuous f :=
  continuous_iff_continuousAt.2 fun x => (em _).elim (hf x) fun hx =>
    (@continuousAt_const _ _ _ _ _ 1).congr (not_mem_mulTSupport_iff_eventuallyEq.mp hx).symm
#align continuous_of_mul_tsupport continuous_of_mulTSupport
#align continuous_of_tsupport continuous_of_tsupport

end

/-! ## Functions with compact support -/
section CompactSupport
variable [TopologicalSpace Œ±] [TopologicalSpace Œ±']
variable [One Œ≤] [One Œ≥] [One Œ¥]
variable {g : Œ≤ ‚Üí Œ≥} {f : Œ± ‚Üí Œ≤} {f‚ÇÇ : Œ± ‚Üí Œ≥} {m : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥} {x : Œ±}

/-- A function `f` *has compact multiplicative support* or is *compactly supported* if the closure
of the multiplicative support of `f` is compact. In a T‚ÇÇ space this is equivalent to `f` being equal
to `1` outside a compact set. -/
@[to_additive " A function `f` *has compact support* or is *compactly supported* if the closure of
the support of `f` is compact. In a T‚ÇÇ space this is equivalent to `f` being equal to `0` outside a
compact set. "]
def HasCompactMulSupport (f : Œ± ‚Üí Œ≤) : Prop :=
  IsCompact (mulTSupport f)
#align has_compact_mul_support HasCompactMulSupport
#align has_compact_support HasCompactSupport

@[to_additive]
theorem hasCompactMulSupport_def : HasCompactMulSupport f ‚Üî IsCompact (closure (mulSupport f)) := by
  rfl
#align has_compact_mul_support_def hasCompactMulSupport_def
#align has_compact_support_def hasCompactSupport_def

@[to_additive]
theorem exists_compact_iff_hasCompactMulSupport [R1Space Œ±] :
    (‚àÉ K : Set Œ±, IsCompact K ‚àß ‚àÄ x, x ‚àâ K ‚Üí f x = 1) ‚Üî HasCompactMulSupport f := by
  simp_rw [‚Üê nmem_mulSupport, ‚Üê mem_compl_iff, ‚Üê subset_def, compl_subset_compl,
    hasCompactMulSupport_def, exists_isCompact_superset_iff]
#align exists_compact_iff_has_compact_mul_support exists_compact_iff_hasCompactMulSupport
#align exists_compact_iff_has_compact_support exists_compact_iff_hasCompactSupport

namespace HasCompactMulSupport
@[to_additive]
theorem intro [R1Space Œ±] {K : Set Œ±} (hK : IsCompact K)
    (hfK : ‚àÄ x, x ‚àâ K ‚Üí f x = 1) : HasCompactMulSupport f :=
  exists_compact_iff_hasCompactMulSupport.mp ‚ü®K, hK, hfK‚ü©
#align has_compact_mul_support.intro HasCompactMulSupport.intro
#align has_compact_support.intro HasCompactSupport.intro

@[to_additive]
theorem intro' {K : Set Œ±} (hK : IsCompact K) (h'K : IsClosed K)
    (hfK : ‚àÄ x, x ‚àâ K ‚Üí f x = 1) : HasCompactMulSupport f := by
  have : mulTSupport f ‚äÜ K := by
    rw [‚Üê h'K.closure_eq]
    apply closure_mono (mulSupport_subset_iff'.2 hfK)
  exact IsCompact.of_isClosed_subset hK ( isClosed_mulTSupport f) this

@[to_additive]
theorem of_mulSupport_subset_isCompact [R1Space Œ±] {K : Set Œ±}
    (hK : IsCompact K) (h : mulSupport f ‚äÜ K) : HasCompactMulSupport f :=
  hK.closure_of_subset h

@[to_additive]
theorem isCompact (hf : HasCompactMulSupport f) : IsCompact (mulTSupport f) :=
  hf
#align has_compact_mul_support.is_compact HasCompactMulSupport.isCompact
#align has_compact_support.is_compact HasCompactSupport.isCompact

@[to_additive]
theorem _root_.hasCompactMulSupport_iff_eventuallyEq :
    HasCompactMulSupport f ‚Üî f =·∂†[coclosedCompact Œ±] 1 :=
  mem_coclosedCompact_iff.symm
#align has_compact_mul_support_iff_eventually_eq hasCompactMulSupport_iff_eventuallyEq
#align has_compact_support_iff_eventually_eq hasCompactSupport_iff_eventuallyEq

@[to_additive]
theorem _root_.isCompact_range_of_mulSupport_subset_isCompact [TopologicalSpace Œ≤]
    (hf : Continuous f) {k : Set Œ±} (hk : IsCompact k) (h'f : mulSupport f ‚äÜ k) :
    IsCompact (range f) := by
  cases' range_eq_image_or_of_mulSupport_subset h'f with h2 h2 <;> rw [h2]
  exacts [hk.image hf, (hk.image hf).insert 1]

@[to_additive]
theorem isCompact_range [TopologicalSpace Œ≤] (h : HasCompactMulSupport f)
    (hf : Continuous f) : IsCompact (range f) :=
  isCompact_range_of_mulSupport_subset_isCompact hf h (subset_mulTSupport f)
#align has_compact_mul_support.is_compact_range HasCompactMulSupport.isCompact_range
#align has_compact_support.is_compact_range HasCompactSupport.isCompact_range

@[to_additive]
theorem mono' {f' : Œ± ‚Üí Œ≥} (hf : HasCompactMulSupport f)
    (hff' : mulSupport f' ‚äÜ mulTSupport f) : HasCompactMulSupport f' :=
  IsCompact.of_isClosed_subset hf isClosed_closure <| closure_minimal hff' isClosed_closure
#align has_compact_mul_support.mono' HasCompactMulSupport.mono'
#align has_compact_support.mono' HasCompactSupport.mono'

@[to_additive]
theorem mono {f' : Œ± ‚Üí Œ≥} (hf : HasCompactMulSupport f)
    (hff' : mulSupport f' ‚äÜ mulSupport f) : HasCompactMulSupport f' :=
  hf.mono' <| hff'.trans subset_closure
#align has_compact_mul_support.mono HasCompactMulSupport.mono
#align has_compact_support.mono HasCompactSupport.mono

@[to_additive]
theorem comp_left (hf : HasCompactMulSupport f) (hg : g 1 = 1) :
    HasCompactMulSupport (g ‚àò f) :=
  hf.mono <| mulSupport_comp_subset hg f
#align has_compact_mul_support.comp_left HasCompactMulSupport.comp_left
#align has_compact_support.comp_left HasCompactSupport.comp_left

@[to_additive]
theorem _root_.hasCompactMulSupport_comp_left (hg : ‚àÄ {x}, g x = 1 ‚Üî x = 1) :
    HasCompactMulSupport (g ‚àò f) ‚Üî HasCompactMulSupport f := by
  simp_rw [hasCompactMulSupport_def, mulSupport_comp_eq g (@hg) f]
#align has_compact_mul_support_comp_left hasCompactMulSupport_comp_left
#align has_compact_support_comp_left hasCompactSupport_comp_left

@[to_additive]
theorem comp_closedEmbedding (hf : HasCompactMulSupport f) {g : Œ±' ‚Üí Œ±}
    (hg : ClosedEmbedding g) : HasCompactMulSupport (f ‚àò g) := by
  rw [hasCompactMulSupport_def, Function.mulSupport_comp_eq_preimage]
  refine IsCompact.of_isClosed_subset (hg.isCompact_preimage hf) isClosed_closure ?_
  rw [hg.toEmbedding.closure_eq_preimage_closure_image]
  exact preimage_mono (closure_mono <| image_preimage_subset _ _)
#align has_compact_mul_support.comp_closed_embedding HasCompactMulSupport.comp_closedEmbedding
#align has_compact_support.comp_closed_embedding HasCompactSupport.comp_closedEmbedding

@[to_additive]
theorem comp‚ÇÇ_left (hf : HasCompactMulSupport f)
    (hf‚ÇÇ : HasCompactMulSupport f‚ÇÇ) (hm : m 1 1 = 1) :
    HasCompactMulSupport fun x => m (f x) (f‚ÇÇ x) := by
  rw [hasCompactMulSupport_iff_eventuallyEq] at hf hf‚ÇÇ ‚ä¢
  #adaptation_note /-- `nightly-2024-03-11`
  If we *either* (1) remove the type annotations on the
  binders in the following `fun` or (2) revert `simp only` to `simp_rw`, `to_additive` fails
  because an `OfNat.ofNat 1` is not replaced with `0`. Notably, as of this nightly, what used to
  look like `OfNat.ofNat (nat_lit 1) x` in the proof term now looks like
  `OfNat.ofNat (OfNat.ofNat (Œ± := ‚Ñï) (nat_lit 1)) x`, and this seems to trip up `to_additive`.
  -/
  filter_upwards [hf, hf‚ÇÇ] using fun x (hx : f x = (1 : Œ± ‚Üí Œ≤) x) (hx‚ÇÇ : f‚ÇÇ x = (1 : Œ± ‚Üí Œ≥) x) => by
    simp only [hx, hx‚ÇÇ, Pi.one_apply, hm]
#align has_compact_mul_support.comp‚ÇÇ_left HasCompactMulSupport.comp‚ÇÇ_left
#align has_compact_support.comp‚ÇÇ_left HasCompactSupport.comp‚ÇÇ_left

@[to_additive]
lemma isCompact_preimage [TopologicalSpace Œ≤]
    (h'f : HasCompactMulSupport f) (hf : Continuous f) {k : Set Œ≤} (hk : IsClosed k)
    (h'k : 1 ‚àâ k) : IsCompact (f ‚Åª¬π' k) := by
  apply IsCompact.of_isClosed_subset h'f (hk.preimage hf) (fun x hx ‚Ü¶ ?_)
  apply subset_mulTSupport
  aesop

variable [T2Space Œ±'] (hf : HasCompactMulSupport f) {g : Œ± ‚Üí Œ±'} (cont : Continuous g)

@[to_additive]
theorem mulTSupport_extend_one_subset :
    mulTSupport (g.extend f 1) ‚äÜ g '' mulTSupport f :=
  (hf.image cont).isClosed.closure_subset_iff.mpr <|
    mulSupport_extend_one_subset.trans (image_subset g subset_closure)

@[to_additive]
theorem extend_one : HasCompactMulSupport (g.extend f 1) :=
  HasCompactMulSupport.of_mulSupport_subset_isCompact (hf.image cont)
    (subset_closure.trans <| hf.mulTSupport_extend_one_subset cont)

@[to_additive]
theorem mulTSupport_extend_one (inj : g.Injective) :
    mulTSupport (g.extend f 1) = g '' mulTSupport f :=
  (hf.mulTSupport_extend_one_subset cont).antisymm <|
    (image_closure_subset_closure_image cont).trans
      (closure_mono (mulSupport_extend_one inj).superset)

@[to_additive]
theorem continuous_extend_one [TopologicalSpace Œ≤] {U : Set Œ±'} (hU : IsOpen U) {f : U ‚Üí Œ≤}
    (cont : Continuous f) (supp : HasCompactMulSupport f) :
    Continuous (Subtype.val.extend f 1) :=
  continuous_of_mulTSupport fun x h ‚Ü¶ by
    rw [show x = ‚Üë(‚ü®x, Subtype.coe_image_subset _ _
      (supp.mulTSupport_extend_one_subset continuous_subtype_val h)‚ü© : U) by rfl,
      ‚Üê (hU.openEmbedding_subtype_val).continuousAt_iff, extend_comp Subtype.val_injective]
    exact cont.continuousAt

end HasCompactMulSupport

end CompactSupport

/-! ## Functions with compact support: algebraic operations -/
section CompactSupport2
section Monoid

variable [TopologicalSpace Œ±] [Monoid Œ≤]
variable {f f' : Œ± ‚Üí Œ≤} {x : Œ±}

@[to_additive]
theorem HasCompactMulSupport.mul (hf : HasCompactMulSupport f) (hf' : HasCompactMulSupport f') :
    HasCompactMulSupport (f * f') := hf.comp‚ÇÇ_left hf' (mul_one 1)
#align has_compact_mul_support.mul HasCompactMulSupport.mul
#align has_compact_support.add HasCompactSupport.add

end Monoid

section DistribMulAction

variable [TopologicalSpace Œ±] [MonoidWithZero R] [AddMonoid M] [DistribMulAction R M]
variable {f : Œ± ‚Üí R} {f' : Œ± ‚Üí M} {x : Œ±}

theorem HasCompactSupport.smul_left (hf : HasCompactSupport f') : HasCompactSupport (f ‚Ä¢ f') := by
  rw [hasCompactSupport_iff_eventuallyEq] at hf ‚ä¢
  exact hf.mono fun x hx => by simp_rw [Pi.smul_apply', hx, Pi.zero_apply, smul_zero]
#align has_compact_support.smul_left HasCompactSupport.smul_left

end DistribMulAction

section SMulWithZero

variable [TopologicalSpace Œ±] [Zero R] [Zero M] [SMulWithZero R M]
variable {f : Œ± ‚Üí R} {f' : Œ± ‚Üí M} {x : Œ±}

theorem HasCompactSupport.smul_right (hf : HasCompactSupport f) : HasCompactSupport (f ‚Ä¢ f') := by
  rw [hasCompactSupport_iff_eventuallyEq] at hf ‚ä¢
  exact hf.mono fun x hx => by simp_rw [Pi.smul_apply', hx, Pi.zero_apply, zero_smul]
#align has_compact_support.smul_right HasCompactSupport.smul_right

theorem HasCompactSupport.smul_left' (hf : HasCompactSupport f') : HasCompactSupport (f ‚Ä¢ f') := by
  rw [hasCompactSupport_iff_eventuallyEq] at hf ‚ä¢
  exact hf.mono fun x hx => by simp_rw [Pi.smul_apply', hx, Pi.zero_apply, smul_zero]
#align has_compact_support.smul_left' HasCompactSupport.smul_left'

end SMulWithZero

section MulZeroClass

variable [TopologicalSpace Œ±] [MulZeroClass Œ≤]
variable {f f' : Œ± ‚Üí Œ≤} {x : Œ±}

theorem HasCompactSupport.mul_right (hf : HasCompactSupport f) : HasCompactSupport (f * f') := by
  rw [hasCompactSupport_iff_eventuallyEq] at hf ‚ä¢
  exact hf.mono fun x hx => by simp_rw [Pi.mul_apply, hx, Pi.zero_apply, zero_mul]
#align has_compact_support.mul_right HasCompactSupport.mul_right

theorem HasCompactSupport.mul_left (hf : HasCompactSupport f') : HasCompactSupport (f * f') := by
  rw [hasCompactSupport_iff_eventuallyEq] at hf ‚ä¢
  exact hf.mono fun x hx => by simp_rw [Pi.mul_apply, hx, Pi.zero_apply, mul_zero]
#align has_compact_support.mul_left HasCompactSupport.mul_left

end MulZeroClass

end CompactSupport2

section LocallyFinite

variable {Œπ : Type*} [TopologicalSpace X]

-- Porting note (#11215): TODO: reformulate for any locally finite family of sets
/-- If a family of functions `f` has locally-finite multiplicative support, subordinate to a family
of open sets, then for any point we can find a neighbourhood on which only finitely-many members of
`f` are not equal to 1. -/
@[to_additive " If a family of functions `f` has locally-finite support, subordinate to a family of
open sets, then for any point we can find a neighbourhood on which only finitely-many members of `f`
are non-zero. "]
theorem LocallyFinite.exists_finset_nhd_mulSupport_subset {U : Œπ ‚Üí Set X} [One R] {f : Œπ ‚Üí X ‚Üí R}
    (hlf : LocallyFinite fun i => mulSupport (f i)) (hso : ‚àÄ i, mulTSupport (f i) ‚äÜ U i)
    (ho : ‚àÄ i, IsOpen (U i)) (x : X) :
    ‚àÉ (is : Finset Œπ), ‚àÉ n, n ‚àà ùìù x ‚àß (n ‚äÜ ‚ãÇ i ‚àà is, U i) ‚àß
      ‚àÄ z ‚àà n, (mulSupport fun i => f i z) ‚äÜ is := by
  obtain ‚ü®n, hn, hnf‚ü© := hlf x
  classical
    let is := hnf.toFinset.filter fun i => x ‚àà U i
    let js := hnf.toFinset.filter fun j => x ‚àâ U j
    refine
      ‚ü®is, (n ‚à© ‚ãÇ j ‚àà js, (mulTSupport (f j))·∂ú) ‚à© ‚ãÇ i ‚àà is, U i, inter_mem (inter_mem hn ?_) ?_,
        inter_subset_right, fun z hz => ?_‚ü©
    ¬∑ exact (biInter_finset_mem js).mpr fun j hj => IsClosed.compl_mem_nhds (isClosed_mulTSupport _)
        (Set.not_mem_subset (hso j) (Finset.mem_filter.mp hj).2)
    ¬∑ exact (biInter_finset_mem is).mpr fun i hi => (ho i).mem_nhds (Finset.mem_filter.mp hi).2
    ¬∑ have hzn : z ‚àà n := by
        rw [inter_assoc] at hz
        exact mem_of_mem_inter_left hz
      replace hz := mem_of_mem_inter_right (mem_of_mem_inter_left hz)
      simp only [js, Finset.mem_filter, Finite.mem_toFinset, mem_setOf_eq, mem_iInter,
        and_imp] at hz
      suffices (mulSupport fun i => f i z) ‚äÜ hnf.toFinset by
        refine hnf.toFinset.subset_coe_filter_of_subset_forall _ this fun i hi => ?_
        specialize hz i ‚ü®z, ‚ü®hi, hzn‚ü©‚ü©
        contrapose hz
        simp [hz, subset_mulTSupport (f i) hi]
      intro i hi
      simp only [Finite.coe_toFinset, mem_setOf_eq]
      exact ‚ü®z, ‚ü®hi, hzn‚ü©‚ü©
#align locally_finite.exists_finset_nhd_mul_support_subset LocallyFinite.exists_finset_nhd_mulSupport_subset
#align locally_finite.exists_finset_nhd_support_subset LocallyFinite.exists_finset_nhd_support_subset

@[to_additive]
theorem locallyFinite_mulSupport_iff [CommMonoid M] {f : Œπ ‚Üí X ‚Üí M} :
    (LocallyFinite fun i ‚Ü¶ mulSupport <| f i) ‚Üî LocallyFinite fun i ‚Ü¶ mulTSupport <| f i :=
  ‚ü®LocallyFinite.closure, fun H ‚Ü¶ H.subset fun _ ‚Ü¶ subset_closure‚ü©

theorem LocallyFinite.smul_left [Zero R] [Zero M] [SMulWithZero R M]
    {s : Œπ ‚Üí X ‚Üí R} (h : LocallyFinite fun i ‚Ü¶ support <| s i) (f : Œπ ‚Üí X ‚Üí M) :
    LocallyFinite fun i ‚Ü¶ support <| s i ‚Ä¢ f i :=
  h.subset fun i x ‚Ü¶ mt <| fun h ‚Ü¶ by rw [Pi.smul_apply', h, zero_smul]

theorem LocallyFinite.smul_right [Zero M] [SMulZeroClass R M]
    {f : Œπ ‚Üí X ‚Üí M} (h : LocallyFinite fun i ‚Ü¶ support <| f i) (s : Œπ ‚Üí X ‚Üí R) :
    LocallyFinite fun i ‚Ü¶ support <| s i ‚Ä¢ f i :=
  h.subset fun i x ‚Ü¶ mt <| fun h ‚Ü¶ by rw [Pi.smul_apply', h, smul_zero]

end LocallyFinite
