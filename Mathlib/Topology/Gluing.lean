/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.CategoryTheory.GlueData
import Mathlib.Topology.Category.TopCat.Limits.Pullbacks
import Mathlib.Topology.Category.TopCat.Opens
import Mathlib.Tactic.Generalize

#align_import topology.gluing from "leanprover-community/mathlib"@"178a32653e369dce2da68dc6b2694e385d484ef1"

/-!
# Gluing Topological spaces

Given a family of gluing data (see `Mathlib/CategoryTheory/GlueData.lean`), we can then glue them
together.

The construction should be "sealed" and considered as a black box, while only using the API
provided.

## Main definitions

* `TopCat.GlueData`: A structure containing the family of gluing data.
* `CategoryTheory.GlueData.glued`: The glued topological space.
    This is defined as the multicoequalizer of `âˆ V i j â‡‰ âˆ U i`, so that the general colimit API
    can be used.
* `CategoryTheory.GlueData.Î¹`: The immersion `Î¹ i : U i âŸ¶ glued` for each `i : Î¹`.
* `TopCat.GlueData.Rel`: A relation on `Î£ i, D.U i` defined by `âŸ¨i, xâŸ© ~ âŸ¨j, yâŸ©` iff
    `âŸ¨i, xâŸ© = âŸ¨j, yâŸ©` or `t i j x = y`. See `TopCat.GlueData.Î¹_eq_iff_rel`.
* `TopCat.GlueData.mk`: A constructor of `GlueData` whose conditions are stated in terms of
  elements rather than subobjects and pullbacks.
* `TopCat.GlueData.ofOpenSubsets`: Given a family of open sets, we may glue them into a new
  topological space. This new space embeds into the original space, and is homeomorphic to it if
  the given family is an open cover (`TopCat.GlueData.openCoverGlueHomeo`).

## Main results

* `TopCat.GlueData.isOpen_iff`: A set in `glued` is open iff its preimage along each `Î¹ i` is
    open.
* `TopCat.GlueData.Î¹_jointly_surjective`: The `Î¹ i`s are jointly surjective.
* `TopCat.GlueData.rel_equiv`: `Rel` is an equivalence relation.
* `TopCat.GlueData.Î¹_eq_iff_rel`: `Î¹ i x = Î¹ j y â†” âŸ¨i, xâŸ© ~ âŸ¨j, yâŸ©`.
* `TopCat.GlueData.image_inter`: The intersection of the images of `U i` and `U j` in `glued` is
    `V i j`.
* `TopCat.GlueData.preimage_range`: The preimage of the image of `U i` in `U j` is `V i j`.
* `TopCat.GlueData.preimage_image_eq_image`: The preimage of the image of some `U âŠ† U i` is
    given by XXX.
* `TopCat.GlueData.Î¹_openEmbedding`: Each of the `Î¹ i`s are open embeddings.

-/

noncomputable section

open TopologicalSpace CategoryTheory

universe v u

open CategoryTheory.Limits

namespace TopCat

/-- A family of gluing data consists of
1. An index type `J`
2. An object `U i` for each `i : J`.
3. An object `V i j` for each `i j : J`.
  (Note that this is `J Ã— J â†’ TopCat` rather than `J â†’ J â†’ TopCat` to connect to the
  limits library easier.)
4. An open embedding `f i j : V i j âŸ¶ U i` for each `i j : Î¹`.
5. A transition map `t i j : V i j âŸ¶ V j i` for each `i j : Î¹`.
such that
6. `f i i` is an isomorphism.
7. `t i i` is the identity.
8. `V i j Ã—[U i] V i k âŸ¶ V i j âŸ¶ V j i` factors through `V j k Ã—[U j] V j i âŸ¶ V j i` via some
    `t' : V i j Ã—[U i] V i k âŸ¶ V j k Ã—[U j] V j i`.
    (This merely means that `V i j âˆ© V i k âŠ† t i j â»Â¹' (V j i âˆ© V j k)`.)
9. `t' i j k â‰« t' j k i â‰« t' k i j = ðŸ™ _`.

We can then glue the topological spaces `U i` together by identifying `V i j` with `V j i`, such
that the `U i`'s are open subspaces of the glued space.

Most of the times it would be easier to use the constructor `TopCat.GlueData.mk'` where the
conditions are stated in a less categorical way.
-/
-- porting note (#5171): removed @[nolint has_nonempty_instance]
structure GlueData extends GlueData TopCat where
  f_open : âˆ€ i j, OpenEmbedding (f i j)
  f_mono := fun i j => (TopCat.mono_iff_injective _).mpr (f_open i j).toEmbedding.inj
set_option linter.uppercaseLean3 false in
#align Top.glue_data TopCat.GlueData

namespace GlueData

variable (D : GlueData.{u})

local notation "ð–£" => D.toGlueData

theorem Ï€_surjective : Function.Surjective ð–£.Ï€ :=
  (TopCat.epi_iff_surjective ð–£.Ï€).mp inferInstance
set_option linter.uppercaseLean3 false in
#align Top.glue_data.Ï€_surjective TopCat.GlueData.Ï€_surjective

theorem isOpen_iff (U : Set ð–£.glued) : IsOpen U â†” âˆ€ i, IsOpen (ð–£.Î¹ i â»Â¹' U) := by
  delta CategoryTheory.GlueData.Î¹
  simp_rw [â† Multicoequalizer.Î¹_sigmaÏ€ ð–£.diagram]
  rw [â† (homeoOfIso (Multicoequalizer.isoCoequalizer ð–£.diagram).symm).isOpen_preimage]
  rw [coequalizer_isOpen_iff]
  dsimp only [GlueData.diagram_l, GlueData.diagram_left, GlueData.diagram_r, GlueData.diagram_right,
    parallelPair_obj_one]
  rw [colimit_isOpen_iff.{_,u}]  -- Porting note: changed `.{u}` to `.{_,u}`.  fun fact: the proof
                                 -- breaks down if this `rw` is merged with the `rw` above.
  constructor
  Â· intro h j; exact h âŸ¨jâŸ©
  Â· intro h j; cases j; apply h
set_option linter.uppercaseLean3 false in
#align Top.glue_data.is_open_iff TopCat.GlueData.isOpen_iff

theorem Î¹_jointly_surjective (x : ð–£.glued) : âˆƒ (i : _) (y : D.U i), ð–£.Î¹ i y = x :=
  ð–£.Î¹_jointly_surjective (forget TopCat) x
set_option linter.uppercaseLean3 false in
#align Top.glue_data.Î¹_jointly_surjective TopCat.GlueData.Î¹_jointly_surjective

/-- An equivalence relation on `Î£ i, D.U i` that holds iff `ð–£ .Î¹ i x = ð–£ .Î¹ j y`.
See `TopCat.GlueData.Î¹_eq_iff_rel`.
-/
def Rel (a b : Î£ i, ((D.U i : TopCat) : Type _)) : Prop :=
  a = b âˆ¨ âˆƒ x : D.V (a.1, b.1), D.f _ _ x = a.2 âˆ§ D.f _ _ (D.t _ _ x) = b.2
set_option linter.uppercaseLean3 false in
#align Top.glue_data.rel TopCat.GlueData.Rel

theorem rel_equiv : Equivalence D.Rel :=
  âŸ¨fun x => Or.inl (refl x), by
    rintro a b (âŸ¨âŸ¨âŸ©âŸ© | âŸ¨x, eâ‚, eâ‚‚âŸ©)
    exacts [Or.inl rfl, Or.inr âŸ¨D.t _ _ x, by simp [eâ‚, eâ‚‚]âŸ©], by
    rintro âŸ¨i, aâŸ© âŸ¨j, bâŸ© âŸ¨k, câŸ© (âŸ¨âŸ¨âŸ©âŸ© | âŸ¨x, eâ‚, eâ‚‚âŸ©)
    Â· exact id
    rintro (âŸ¨âŸ¨âŸ©âŸ© | âŸ¨y, eâ‚ƒ, eâ‚„âŸ©)
    Â· exact Or.inr âŸ¨x, eâ‚, eâ‚‚âŸ©
    let z := (pullbackIsoProdSubtype (D.f j i) (D.f j k)).inv âŸ¨âŸ¨_, _âŸ©, eâ‚‚.trans eâ‚ƒ.symmâŸ©
    have eqâ‚ : (D.t j i) ((pullback.fst : _ /-(D.f j k)-/ âŸ¶ D.V (j, i)) z) = x := by simp [z]
    have eqâ‚‚ : (pullback.snd : _ âŸ¶ D.V _) z = y := pullbackIsoProdSubtype_inv_snd_apply _ _ _
    clear_value z
    right
    use (pullback.fst : _ âŸ¶ D.V (i, k)) (D.t' _ _ _ z)
    dsimp only at *
    substs eqâ‚ eqâ‚‚ eâ‚ eâ‚ƒ eâ‚„
    have hâ‚ : D.t' j i k â‰« pullback.fst â‰« D.f i k = pullback.fst â‰« D.t j i â‰« D.f i j := by
      rw [â† ð–£.t_fac_assoc]; congr 1; exact pullback.condition
    have hâ‚‚ : D.t' j i k â‰« pullback.fst â‰« D.t i k â‰« D.f k i = pullback.snd â‰« D.t j k â‰« D.f k j := by
      rw [â† ð–£.t_fac_assoc]
      apply @Epi.left_cancellation _ _ _ _ (D.t' k j i)
      rw [ð–£.cocycle_assoc, ð–£.t_fac_assoc, ð–£.t_inv_assoc]
      exact pullback.condition.symm
    exact âŸ¨ContinuousMap.congr_fun hâ‚ z, ContinuousMap.congr_fun hâ‚‚ zâŸ©âŸ©
set_option linter.uppercaseLean3 false in
#align Top.glue_data.rel_equiv TopCat.GlueData.rel_equiv

open CategoryTheory.Limits.WalkingParallelPair

theorem eqvGen_of_Ï€_eq
    -- Porting note: was `{x y : âˆ D.U} (h : ð–£.Ï€ x = ð–£.Ï€ y)`
    {x y : sigmaObj (Î² := D.toGlueData.J) (C := TopCat) D.toGlueData.U}
    (h : ð–£.Ï€ x = ð–£.Ï€ y) :
    EqvGen
      -- Porting note: was (Types.CoequalizerRel ð–£.diagram.fstSigmaMap ð–£.diagram.sndSigmaMap)
      (Types.CoequalizerRel
        (X := sigmaObj (Î² := D.toGlueData.diagram.L) (C := TopCat) (D.toGlueData.diagram).left)
        (Y := sigmaObj (Î² := D.toGlueData.diagram.R) (C := TopCat) (D.toGlueData.diagram).right)
        ð–£.diagram.fstSigmaMap ð–£.diagram.sndSigmaMap)
      x y := by
  delta GlueData.Ï€ Multicoequalizer.sigmaÏ€ at h
  -- Porting note: inlined `inferInstance` instead of leaving as a side goal.
  replace h := (TopCat.mono_iff_injective (Multicoequalizer.isoCoequalizer ð–£.diagram).inv).mp
    inferInstance h
  let diagram := parallelPair ð–£.diagram.fstSigmaMap ð–£.diagram.sndSigmaMap â‹™ forget _
  have : colimit.Î¹ diagram one x = colimit.Î¹ diagram one y := by
    dsimp only [coequalizer.Ï€, ContinuousMap.toFun_eq_coe] at h
    -- This used to be `rw`, but we need `erw` after leanprover/lean4#2644
    erw [â† Î¹_preservesColimitsIso_hom, forget_map_eq_coe, types_comp_apply, h]
    simp
    rfl
  have :
    (colimit.Î¹ diagram _ â‰« colim.map _ â‰« (colimit.isoColimitCocone _).hom) _ =
      (colimit.Î¹ diagram _ â‰« colim.map _ â‰« (colimit.isoColimitCocone _).hom) _ :=
    (congr_arg
        (colim.map (diagramIsoParallelPair diagram).hom â‰«
          (colimit.isoColimitCocone (Types.coequalizerColimit _ _)).hom)
        this :
      _)
  -- Porting note: was
  -- simp only [eqToHom_refl, types_comp_apply, colimit.Î¹_map_assoc,
  --   diagramIsoParallelPair_hom_app, colimit.isoColimitCocone_Î¹_hom, types_id_apply] at this
  -- See https://github.com/leanprover-community/mathlib4/issues/5026
  rw [colimit.Î¹_map_assoc, diagramIsoParallelPair_hom_app, eqToHom_refl,
    colimit.isoColimitCocone_Î¹_hom, types_comp_apply, types_id_apply, types_comp_apply,
    types_id_apply] at this
  exact Quot.eq.1 this
set_option linter.uppercaseLean3 false in
#align Top.glue_data.eqv_gen_of_Ï€_eq TopCat.GlueData.eqvGen_of_Ï€_eq

theorem Î¹_eq_iff_rel (i j : D.J) (x : D.U i) (y : D.U j) :
    ð–£.Î¹ i x = ð–£.Î¹ j y â†” D.Rel âŸ¨i, xâŸ© âŸ¨j, yâŸ© := by
  constructor
  Â· delta GlueData.Î¹
    simp_rw [â† Multicoequalizer.Î¹_sigmaÏ€]
    intro h
    rw [â†
      show _ = Sigma.mk i x from ConcreteCategory.congr_hom (sigmaIsoSigma.{_, u} D.U).inv_hom_id _]
    rw [â†
      show _ = Sigma.mk j y from ConcreteCategory.congr_hom (sigmaIsoSigma.{_, u} D.U).inv_hom_id _]
    change InvImage D.Rel (sigmaIsoSigma.{_, u} D.U).hom _ _
    rw [â† (InvImage.equivalence _ _ D.rel_equiv).eqvGen_iff]
    refine' EqvGen.mono _ (D.eqvGen_of_Ï€_eq h : _)
    rintro _ _ âŸ¨xâŸ©
    obtain âŸ¨âŸ¨âŸ¨i, jâŸ©, yâŸ©, rflâŸ© :=
      (ConcreteCategory.bijective_of_isIso (sigmaIsoSigma.{u, u} _).inv).2 x
    unfold InvImage MultispanIndex.fstSigmaMap MultispanIndex.sndSigmaMap
    simp only [forget_map_eq_coe, Opens.inclusion_apply, TopCat.comp_app, sigmaIsoSigma_inv_apply,
      Cofan.mk_Î¹_app]
    rw [â† comp_apply, colimit.Î¹_desc, â† comp_apply, colimit.Î¹_desc]
    erw [sigmaIsoSigma_hom_Î¹_apply, sigmaIsoSigma_hom_Î¹_apply]
    exact Or.inr âŸ¨y, âŸ¨rfl, rflâŸ©âŸ©
  Â· rintro (âŸ¨âŸ¨âŸ©âŸ© | âŸ¨z, eâ‚, eâ‚‚âŸ©)
    Â· rfl
    dsimp only at *
    -- Porting note: there were `subst eâ‚` and `subst eâ‚‚`, instead of the `rw`
    rw [â† eâ‚, â† eâ‚‚] at *
    simp
set_option linter.uppercaseLean3 false in
#align Top.glue_data.Î¹_eq_iff_rel TopCat.GlueData.Î¹_eq_iff_rel

theorem Î¹_injective (i : D.J) : Function.Injective (ð–£.Î¹ i) := by
  intro x y h
  rcases (D.Î¹_eq_iff_rel _ _ _ _).mp h with (âŸ¨âŸ¨âŸ©âŸ© | âŸ¨_, eâ‚, eâ‚‚âŸ©)
  Â· rfl
  Â· dsimp only at *
    -- Porting note: there were `cases eâ‚` and `cases eâ‚‚`, instead of the `rw`
    rw [â† eâ‚, â† eâ‚‚]
    simp
set_option linter.uppercaseLean3 false in
#align Top.glue_data.Î¹_injective TopCat.GlueData.Î¹_injective

instance Î¹_mono (i : D.J) : Mono (ð–£.Î¹ i) :=
  (TopCat.mono_iff_injective _).mpr (D.Î¹_injective _)
set_option linter.uppercaseLean3 false in
#align Top.glue_data.Î¹_mono TopCat.GlueData.Î¹_mono

theorem image_inter (i j : D.J) :
    Set.range (ð–£.Î¹ i) âˆ© Set.range (ð–£.Î¹ j) = Set.range (D.f i j â‰« ð–£.Î¹ _) := by
  ext x
  constructor
  Â· rintro âŸ¨âŸ¨xâ‚, eqâ‚âŸ©, âŸ¨xâ‚‚, eqâ‚‚âŸ©âŸ©
    obtain âŸ¨âŸ¨âŸ©âŸ© | âŸ¨y, eâ‚, -âŸ© := (D.Î¹_eq_iff_rel _ _ _ _).mp (eqâ‚.trans eqâ‚‚.symm)
    Â· exact âŸ¨inv (D.f i i) xâ‚, by
        -- porting note (#10745): was `simp [eqâ‚]`
        -- See https://github.com/leanprover-community/mathlib4/issues/5026
        rw [TopCat.comp_app]
        erw [CategoryTheory.IsIso.inv_hom_id_apply]
        rw [eqâ‚]âŸ©
    Â· -- Porting note: was
      -- dsimp only at *; substs eâ‚ eqâ‚; exact âŸ¨y, by simpâŸ©
      dsimp only at *
      substs eqâ‚
      exact âŸ¨y, by simp [eâ‚]âŸ©
  Â· rintro âŸ¨x, hxâŸ©
    exact âŸ¨âŸ¨D.f i j x, hxâŸ©, âŸ¨D.f j i (D.t _ _ x), by simp [â† hx]âŸ©âŸ©
set_option linter.uppercaseLean3 false in
#align Top.glue_data.image_inter TopCat.GlueData.image_inter

theorem preimage_range (i j : D.J) : ð–£.Î¹ j â»Â¹' Set.range (ð–£.Î¹ i) = Set.range (D.f j i) := by
  rw [â† Set.preimage_image_eq (Set.range (D.f j i)) (D.Î¹_injective j), â† Set.image_univ, â†
    Set.image_univ, â† Set.image_comp, â† coe_comp, Set.image_univ, Set.image_univ, â† image_inter,
    Set.preimage_range_inter]
set_option linter.uppercaseLean3 false in
#align Top.glue_data.preimage_range TopCat.GlueData.preimage_range

theorem preimage_image_eq_image (i j : D.J) (U : Set (ð–£.U i)) :
    ð–£.Î¹ j â»Â¹' (ð–£.Î¹ i '' U) = D.f _ _ '' ((D.t j i â‰« D.f _ _) â»Â¹' U) := by
  have : D.f _ _ â»Â¹' (ð–£.Î¹ j â»Â¹' (ð–£.Î¹ i '' U)) = (D.t j i â‰« D.f _ _) â»Â¹' U := by
    ext x
    conv_rhs => rw [â† Set.preimage_image_eq U (D.Î¹_injective _)]
    generalize ð–£.Î¹ i '' U = U'
    simp
  rw [â† this, Set.image_preimage_eq_inter_range]
  symm
  apply Set.inter_eq_self_of_subset_left
  rw [â† D.preimage_range i j]
  exact Set.preimage_mono (Set.image_subset_range _ _)
set_option linter.uppercaseLean3 false in
#align Top.glue_data.preimage_image_eq_image TopCat.GlueData.preimage_image_eq_image

theorem preimage_image_eq_image' (i j : D.J) (U : Set (ð–£.U i)) :
    ð–£.Î¹ j â»Â¹' (ð–£.Î¹ i '' U) = (D.t i j â‰« D.f _ _) '' (D.f _ _ â»Â¹' U) := by
  convert D.preimage_image_eq_image i j U using 1
  rw [coe_comp, coe_comp]
  -- Porting note: `show` was not needed, since `rw [â† Set.image_image]` worked.
  show (fun x => ((forget TopCat).map _ ((forget TopCat).map _ x))) '' _ = _
  rw [â† Set.image_image]
  -- Porting note: `congr 1` was here, instead of `congr_arg`, however, it did nothing.
  refine congr_arg ?_ ?_
  rw [â† Set.eq_preimage_iff_image_eq, Set.preimage_preimage]
  Â· change _ = (D.t i j â‰« D.t j i â‰« _) â»Â¹' _
    rw [ð–£.t_inv_assoc]
  rw [â† isIso_iff_bijective]
  apply (forget TopCat).map_isIso
set_option linter.uppercaseLean3 false in
#align Top.glue_data.preimage_image_eq_image' TopCat.GlueData.preimage_image_eq_image'

-- Porting note: the goal was simply `IsOpen (ð–£.Î¹ i '' U)`.
-- I had to manually add the explicit type ascription.
theorem open_image_open (i : D.J) (U : Opens (ð–£.U i)) : IsOpen (ð–£.Î¹ i '' (U : Set (D.U i))) := by
  rw [isOpen_iff]
  intro j
  rw [preimage_image_eq_image]
  apply (D.f_open _ _).isOpenMap
  apply (D.t j i â‰« D.f i j).continuous_toFun.isOpen_preimage
  exact U.isOpen
set_option linter.uppercaseLean3 false in
#align Top.glue_data.open_image_open TopCat.GlueData.open_image_open

theorem Î¹_openEmbedding (i : D.J) : OpenEmbedding (ð–£.Î¹ i) :=
  openEmbedding_of_continuous_injective_open (ð–£.Î¹ i).continuous_toFun (D.Î¹_injective i) fun U h =>
    D.open_image_open i âŸ¨U, hâŸ©
set_option linter.uppercaseLean3 false in
#align Top.glue_data.Î¹_open_embedding TopCat.GlueData.Î¹_openEmbedding

/-- A family of gluing data consists of
1. An index type `J`
2. A bundled topological space `U i` for each `i : J`.
3. An open set `V i j âŠ† U i` for each `i j : J`.
4. A transition map `t i j : V i j âŸ¶ V j i` for each `i j : Î¹`.
such that
6. `V i i = U i`.
7. `t i i` is the identity.
8. For each `x âˆˆ V i j âˆ© V i k`, `t i j x âˆˆ V j k`.
9. `t j k (t i j x) = t i k x`.

We can then glue the topological spaces `U i` together by identifying `V i j` with `V j i`.
-/
-- Porting note(#5171): removed `@[nolint has_nonempty_instance]`
structure MkCore where
  {J : Type u}
  U : J â†’ TopCat.{u}
  V : âˆ€ i, J â†’ Opens (U i)
  t : âˆ€ i j, (Opens.toTopCat _).obj (V i j) âŸ¶ (Opens.toTopCat _).obj (V j i)
  V_id : âˆ€ i, V i i = âŠ¤
  t_id : âˆ€ i, â‡‘(t i i) = id
  t_inter : âˆ€ â¦ƒi jâ¦„ (k) (x : V i j), â†‘x âˆˆ V i k â†’ (((â†‘) : (V j i) â†’ (U j)) (t i j x)) âˆˆ V j k
  cocycle :
    âˆ€ (i j k) (x : V i j) (h : â†‘x âˆˆ V i k),
      -- Porting note: the underscore in the next line was `â†‘(t i j x)`, but Lean type-mismatched
      (((â†‘) : (V k j) â†’ (U k)) (t j k âŸ¨_, t_inter k x hâŸ©)) = ((â†‘) : (V k i) â†’ (U k)) (t i k âŸ¨x, hâŸ©)
set_option linter.uppercaseLean3 false in
#align Top.glue_data.mk_core TopCat.GlueData.MkCore

theorem MkCore.t_inv (h : MkCore) (i j : h.J) (x : h.V j i) : h.t i j ((h.t j i) x) = x := by
  have := h.cocycle j i j x ?_
  Â· rw [h.t_id] at this
    Â· convert Subtype.eq this
    rw [h.V_id]
    trivial
set_option linter.uppercaseLean3 false in
#align Top.glue_data.mk_core.t_inv TopCat.GlueData.MkCore.t_inv

instance (h : MkCore.{u}) (i j : h.J) : IsIso (h.t i j) := by
  use h.t j i; constructor <;> ext1; exacts [h.t_inv _ _ _, h.t_inv _ _ _]

/-- (Implementation) the restricted transition map to be fed into `TopCat.GlueData`. -/
def MkCore.t' (h : MkCore.{u}) (i j k : h.J) :
    pullback (h.V i j).inclusion (h.V i k).inclusion âŸ¶
      pullback (h.V j k).inclusion (h.V j i).inclusion := by
  refine' (pullbackIsoProdSubtype _ _).hom â‰« âŸ¨_, _âŸ© â‰« (pullbackIsoProdSubtype _ _).inv
  Â· intro x
    refine' âŸ¨âŸ¨âŸ¨(h.t i j x.1.1).1, _âŸ©, h.t i j x.1.1âŸ©, rflâŸ©
    rcases x with âŸ¨âŸ¨âŸ¨x, hxâŸ©, âŸ¨x', hx'âŸ©âŸ©, rfl : x = x'âŸ©
    exact h.t_inter _ âŸ¨x, hxâŸ© hx'
  -- Porting note: was `continuity`, see https://github.com/leanprover-community/mathlib4/issues/5030
  have : Continuous (h.t i j) := map_continuous (self := ContinuousMap.toContinuousMapClass) _
  set_option tactic.skipAssignedInstances false in
  exact ((Continuous.subtype_mk (by continuity) _).prod_mk (by continuity)).subtype_mk _

set_option linter.uppercaseLean3 false in
#align Top.glue_data.mk_core.t' TopCat.GlueData.MkCore.t'

/-- This is a constructor of `TopCat.GlueData` whose arguments are in terms of elements and
intersections rather than subobjects and pullbacks. Please refer to `TopCat.GlueData.MkCore` for
details. -/
def mk' (h : MkCore.{u}) : TopCat.GlueData where
  J := h.J
  U := h.U
  V i := (Opens.toTopCat _).obj (h.V i.1 i.2)
  f i j := (h.V i j).inclusion
  f_id i := by
    -- Porting note (#12129): additional beta reduction needed
    beta_reduce
    exact (h.V_id i).symm â–¸ IsIso.of_iso (Opens.inclusionTopIso (h.U i))
  f_open := fun i j : h.J => (h.V i j).openEmbedding
  t := h.t
  t_id i := by ext; rw [h.t_id]; rfl
  t' := h.t'
  t_fac i j k := by
    delta MkCore.t'
    rw [Category.assoc, Category.assoc, pullbackIsoProdSubtype_inv_snd, â† Iso.eq_inv_comp,
      pullbackIsoProdSubtype_inv_fst_assoc]
    ext âŸ¨âŸ¨âŸ¨x, hxâŸ©, âŸ¨x', hx'âŸ©âŸ©, rfl : x = x'âŸ©
    rfl
  cocycle i j k := by
    delta MkCore.t'
    simp_rw [â† Category.assoc]
    rw [Iso.comp_inv_eq]
    simp only [Iso.inv_hom_id_assoc, Category.assoc, Category.id_comp]
    rw [â† Iso.eq_inv_comp, Iso.inv_hom_id]
    ext1 âŸ¨âŸ¨âŸ¨x, hxâŸ©, âŸ¨x', hx'âŸ©âŸ©, rfl : x = x'âŸ©
    -- The next 9 tactics (up to `convert ...` were a single `rw` before leanprover/lean4#2644
    -- rw [comp_app, ContinuousMap.coe_mk, comp_app, id_app, ContinuousMap.coe_mk, Subtype.mk_eq_mk,
    --   Prod.mk.inj_iff, Subtype.mk_eq_mk, Subtype.ext_iff, and_self_iff]
    rw [comp_app] --, comp_app, id_app]
    -- erw [ContinuousMap.coe_mk]
    conv_lhs => erw [ContinuousMap.coe_mk]
    erw [id_app]
    rw [ContinuousMap.coe_mk]
    erw [Subtype.mk_eq_mk]
    rw [Prod.mk.inj_iff]
    erw [Subtype.mk_eq_mk]
    rw [Subtype.ext_iff]
    rw [and_self_iff]
    convert congr_arg Subtype.val (h.t_inv k i âŸ¨x, hx'âŸ©) using 3
    refine Subtype.ext ?_
    exact h.cocycle i j k âŸ¨x, hxâŸ© hx'
  -- Porting note: was not necessary in mathlib3
  f_mono i j := (TopCat.mono_iff_injective _).mpr fun x y h => Subtype.ext h
set_option linter.uppercaseLean3 false in
#align Top.glue_data.mk' TopCat.GlueData.mk'

variable {Î± : Type u} [TopologicalSpace Î±] {J : Type u} (U : J â†’ Opens Î±)

/-- We may construct a glue data from a family of open sets. -/
@[simps! toGlueData_J toGlueData_U toGlueData_V toGlueData_t toGlueData_f]
def ofOpenSubsets : TopCat.GlueData.{u} :=
  mk'.{u}
    { J
      U := fun i => (Opens.toTopCat <| TopCat.of Î±).obj (U i)
      V := fun i j => (Opens.map <| Opens.inclusion _).obj (U j)
      t := fun i j => âŸ¨fun x => âŸ¨âŸ¨x.1.1, x.2âŸ©, x.1.2âŸ©, by
        -- Porting note: was `continuity`, see https://github.com/leanprover-community/mathlib4/issues/5030
        refine Continuous.subtype_mk ?_ ?_
        refine Continuous.subtype_mk ?_ ?_
        continuityâŸ©
      V_id := fun i => by
        ext
        -- Porting note: no longer needed `cases U i`!
        simp
      t_id := fun i => by ext; rfl
      t_inter := fun i j k x hx => hx
      cocycle := fun i j k x h => rfl }
set_option linter.uppercaseLean3 false in
#align Top.glue_data.of_open_subsets TopCat.GlueData.ofOpenSubsets

/-- The canonical map from the glue of a family of open subsets `Î±` into `Î±`.
This map is an open embedding (`fromOpenSubsetsGlue_openEmbedding`),
and its range is `â‹ƒ i, (U i : Set Î±)` (`range_fromOpenSubsetsGlue`).
-/
def fromOpenSubsetsGlue : (ofOpenSubsets U).toGlueData.glued âŸ¶ TopCat.of Î± :=
  Multicoequalizer.desc _ _ (fun x => Opens.inclusion _) (by rintro âŸ¨i, jâŸ©; ext x; rfl)
set_option linter.uppercaseLean3 false in
#align Top.glue_data.from_open_subsets_glue TopCat.GlueData.fromOpenSubsetsGlue

-- Porting note: `elementwise` here produces a bad lemma,
-- where too much has been simplified, despite the `nosimp`.
@[simp, elementwise nosimp]
theorem Î¹_fromOpenSubsetsGlue (i : J) :
    (ofOpenSubsets U).toGlueData.Î¹ i â‰« fromOpenSubsetsGlue U = Opens.inclusion _ :=
  Multicoequalizer.Ï€_desc _ _ _ _ _
set_option linter.uppercaseLean3 false in
#align Top.glue_data.Î¹_from_open_subsets_glue TopCat.GlueData.Î¹_fromOpenSubsetsGlue

theorem fromOpenSubsetsGlue_injective : Function.Injective (fromOpenSubsetsGlue U) := by
  intro x y e
  obtain âŸ¨i, âŸ¨x, hxâŸ©, rflâŸ© := (ofOpenSubsets U).Î¹_jointly_surjective x
  obtain âŸ¨j, âŸ¨y, hyâŸ©, rflâŸ© := (ofOpenSubsets U).Î¹_jointly_surjective y
  -- Porting note: now it is `erw`, it was `rw`
  -- see the porting note on `Î¹_fromOpenSubsetsGlue`
  erw [Î¹_fromOpenSubsetsGlue_apply, Î¹_fromOpenSubsetsGlue_apply] at e
  change x = y at e
  subst e
  rw [(ofOpenSubsets U).Î¹_eq_iff_rel]
  right
  exact âŸ¨âŸ¨âŸ¨x, hxâŸ©, hyâŸ©, rfl, rflâŸ©
set_option linter.uppercaseLean3 false in
#align Top.glue_data.from_open_subsets_glue_injective TopCat.GlueData.fromOpenSubsetsGlue_injective

theorem fromOpenSubsetsGlue_isOpenMap : IsOpenMap (fromOpenSubsetsGlue U) := by
  intro s hs
  rw [(ofOpenSubsets U).isOpen_iff] at hs
  rw [isOpen_iff_forall_mem_open]
  rintro _ âŸ¨x, hx, rflâŸ©
  obtain âŸ¨i, âŸ¨x, hx'âŸ©, rflâŸ© := (ofOpenSubsets U).Î¹_jointly_surjective x
  use fromOpenSubsetsGlue U '' s âˆ© Set.range (@Opens.inclusion (TopCat.of Î±) (U i))
  use Set.inter_subset_left _ _
  constructor
  Â· erw [â† Set.image_preimage_eq_inter_range]
    apply (Opens.openEmbedding (X := TopCat.of Î±) (U i)).isOpenMap
    convert hs i using 1
    erw [â† Î¹_fromOpenSubsetsGlue, coe_comp, Set.preimage_comp]
    --  porting note: `congr 1` did nothing, so I replaced it with `apply congr_arg`
    apply congr_arg
    exact Set.preimage_image_eq _ (fromOpenSubsetsGlue_injective U)
  Â· refine' âŸ¨Set.mem_image_of_mem _ hx, _âŸ©
    -- Porting note: another `rw â†¦ erw`
    -- See above.
    erw [Î¹_fromOpenSubsetsGlue_apply]
    exact Set.mem_range_self _
set_option linter.uppercaseLean3 false in
#align Top.glue_data.from_open_subsets_glue_is_open_map TopCat.GlueData.fromOpenSubsetsGlue_isOpenMap

theorem fromOpenSubsetsGlue_openEmbedding : OpenEmbedding (fromOpenSubsetsGlue U) :=
  openEmbedding_of_continuous_injective_open (ContinuousMap.continuous_toFun _)
    (fromOpenSubsetsGlue_injective U) (fromOpenSubsetsGlue_isOpenMap U)
set_option linter.uppercaseLean3 false in
#align Top.glue_data.from_open_subsets_glue_open_embedding TopCat.GlueData.fromOpenSubsetsGlue_openEmbedding

theorem range_fromOpenSubsetsGlue : Set.range (fromOpenSubsetsGlue U) = â‹ƒ i, (U i : Set Î±) := by
  ext
  constructor
  Â· rintro âŸ¨x, rflâŸ©
    obtain âŸ¨i, âŸ¨x, hx'âŸ©, rflâŸ© := (ofOpenSubsets U).Î¹_jointly_surjective x
    -- Porting note: another `rw â†¦ erw`
    -- See above
    erw [Î¹_fromOpenSubsetsGlue_apply]
    exact Set.subset_iUnion _ i hx'
  Â· rintro âŸ¨_, âŸ¨i, rflâŸ©, hxâŸ©
    rename_i x
    exact âŸ¨(ofOpenSubsets U).toGlueData.Î¹ i âŸ¨x, hxâŸ©, Î¹_fromOpenSubsetsGlue_apply _ _ _âŸ©
set_option linter.uppercaseLean3 false in
#align Top.glue_data.range_from_open_subsets_glue TopCat.GlueData.range_fromOpenSubsetsGlue

/-- The gluing of an open cover is homeomomorphic to the original space. -/
def openCoverGlueHomeo (h : â‹ƒ i, (U i : Set Î±) = Set.univ) :
    (ofOpenSubsets U).toGlueData.glued â‰ƒâ‚œ Î± :=
  Equiv.ofBijective (fromOpenSubsetsGlue U) âŸ¨fromOpenSubsetsGlue_injective U,
      Set.range_iff_surjective.mp ((range_fromOpenSubsetsGlue U).symm â–¸ h)âŸ©
    |>.toHomeomorphOfContinuousOpen (fromOpenSubsetsGlue U).2 (fromOpenSubsetsGlue_isOpenMap U)
set_option linter.uppercaseLean3 false in
#align Top.glue_data.open_cover_glue_homeo TopCat.GlueData.openCoverGlueHomeo

end GlueData

end TopCat
