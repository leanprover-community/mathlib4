/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.CategoryTheory.GlueData
import Mathlib.Topology.Category.TopCat.Limits.Pullbacks
import Mathlib.Topology.Category.TopCat.Opens
import Mathlib.Tactic.Generalize
import Mathlib.CategoryTheory.Elementwise
import Mathlib.CategoryTheory.ConcreteCategory.EpiMono

/-!
# Gluing Topological spaces

Given a family of gluing data (see `Mathlib/CategoryTheory/GlueData.lean`), we can then glue them
together.

The construction should be "sealed" and considered as a black box, while only using the API
provided.

## Main definitions

* `TopCat.GlueData`: A structure containing the family of gluing data.
* `CategoryTheory.GlueData.glued`: The glued topological space.
    This is defined as the multicoequalizer of `‚àê V i j ‚áâ ‚àê U i`, so that the general colimit API
    can be used.
* `CategoryTheory.GlueData.Œπ`: The immersion `Œπ i : U i ‚ü∂ glued` for each `i : Œπ`.
* `TopCat.GlueData.Rel`: A relation on `Œ£ i, D.U i` defined by `‚ü®i, x‚ü© ~ ‚ü®j, y‚ü©` iff
    `‚ü®i, x‚ü© = ‚ü®j, y‚ü©` or `t i j x = y`. See `TopCat.GlueData.Œπ_eq_iff_rel`.
* `TopCat.GlueData.mk`: A constructor of `GlueData` whose conditions are stated in terms of
  elements rather than subobjects and pullbacks.
* `TopCat.GlueData.ofOpenSubsets`: Given a family of open sets, we may glue them into a new
  topological space. This new space embeds into the original space, and is homeomorphic to it if
  the given family is an open cover (`TopCat.GlueData.openCoverGlueHomeo`).

## Main results

* `TopCat.GlueData.isOpen_iff`: A set in `glued` is open iff its preimage along each `Œπ i` is
    open.
* `TopCat.GlueData.Œπ_jointly_surjective`: The `Œπ i`s are jointly surjective.
* `TopCat.GlueData.rel_equiv`: `Rel` is an equivalence relation.
* `TopCat.GlueData.Œπ_eq_iff_rel`: `Œπ i x = Œπ j y ‚Üî ‚ü®i, x‚ü© ~ ‚ü®j, y‚ü©`.
* `TopCat.GlueData.image_inter`: The intersection of the images of `U i` and `U j` in `glued` is
    `V i j`.
* `TopCat.GlueData.preimage_range`: The preimage of the image of `U i` in `U j` is `V i j`.
* `TopCat.GlueData.preimage_image_eq_image`: The preimage of the image of some `U ‚äÜ U i` is
    given by XXX.
* `TopCat.GlueData.Œπ_isOpenEmbedding`: Each of the `Œπ i`s are open embeddings.

-/

noncomputable section

open CategoryTheory TopologicalSpace Topology

universe v u

open CategoryTheory.Limits

namespace TopCat

/-- A family of gluing data consists of
1. An index type `J`
2. An object `U i` for each `i : J`.
3. An object `V i j` for each `i j : J`.
  (Note that this is `J √ó J ‚Üí TopCat` rather than `J ‚Üí J ‚Üí TopCat` to connect to the
  limits library easier.)
4. An open embedding `f i j : V i j ‚ü∂ U i` for each `i j : Œπ`.
5. A transition map `t i j : V i j ‚ü∂ V j i` for each `i j : Œπ`.
such that
6. `f i i` is an isomorphism.
7. `t i i` is the identity.
8. `V i j √ó[U i] V i k ‚ü∂ V i j ‚ü∂ V j i` factors through `V j k √ó[U j] V j i ‚ü∂ V j i` via some
    `t' : V i j √ó[U i] V i k ‚ü∂ V j k √ó[U j] V j i`.
    (This merely means that `V i j ‚à© V i k ‚äÜ t i j ‚Åª¬π' (V j i ‚à© V j k)`.)
9. `t' i j k ‚â´ t' j k i ‚â´ t' k i j = ùüô _`.

We can then glue the topological spaces `U i` together by identifying `V i j` with `V j i`, such
that the `U i`'s are open subspaces of the glued space.

Most of the times it would be easier to use the constructor `TopCat.GlueData.mk'` where the
conditions are stated in a less categorical way.
-/
structure GlueData extends CategoryTheory.GlueData TopCat where
  f_open : ‚àÄ i j, IsOpenEmbedding (f i j)
  f_mono i j := (TopCat.mono_iff_injective _).mpr (f_open i j).isEmbedding.injective

namespace GlueData

variable (D : GlueData.{u})

local notation "ùñ£" => D.toGlueData

theorem œÄ_surjective : Function.Surjective ùñ£.œÄ :=
  (TopCat.epi_iff_surjective ùñ£.œÄ).mp inferInstance

theorem isOpen_iff (U : Set ùñ£.glued) : IsOpen U ‚Üî ‚àÄ i, IsOpen (ùñ£.Œπ i ‚Åª¬π' U) := by
  delta CategoryTheory.GlueData.Œπ
  simp_rw [‚Üê Multicoequalizer.Œπ_sigmaœÄ ùñ£.diagram]
  rw [‚Üê (homeoOfIso (Multicoequalizer.isoCoequalizer ùñ£.diagram).symm).isOpen_preimage]
  rw [coequalizer_isOpen_iff, colimit_isOpen_iff.{u}]
  tauto

theorem Œπ_jointly_surjective (x : ùñ£.glued) : ‚àÉ (i : _) (y : D.U i), ùñ£.Œπ i y = x :=
  ùñ£.Œπ_jointly_surjective (forget TopCat) x

/-- An equivalence relation on `Œ£ i, D.U i` that holds iff `ùñ£.Œπ i x = ùñ£.Œπ j y`.
See `TopCat.GlueData.Œπ_eq_iff_rel`.
-/
def Rel (a b : Œ£ i, ((D.U i : TopCat) : Type _)) : Prop :=
  ‚àÉ x : D.V (a.1, b.1), D.f _ _ x = a.2 ‚àß D.f _ _ (D.t _ _ x) = b.2

theorem rel_equiv : Equivalence D.Rel :=
  ‚ü®fun x => ‚ü®inv (D.f _ _) x.2, IsIso.inv_hom_id_apply (D.f x.fst x.fst) _,
    -- Use `elementwise_of%` elaborator instead of `IsIso.inv_hom_id_apply` to work around
    -- `ConcreteCategory`/`HasForget` mismatch:
    by simp [elementwise_of% IsIso.inv_hom_id (D.f x.fst x.fst)]‚ü©, by
    rintro a b ‚ü®x, e‚ÇÅ, e‚ÇÇ‚ü©
    exact ‚ü®D.t _ _ x, e‚ÇÇ, by rw [‚Üê e‚ÇÅ, D.t_inv_apply]‚ü©, by
    rintro ‚ü®i, a‚ü© ‚ü®j, b‚ü© ‚ü®k, c‚ü© ‚ü®x, e‚ÇÅ, e‚ÇÇ‚ü©
    rintro ‚ü®y, e‚ÇÉ, e‚ÇÑ‚ü©
    let z := (pullbackIsoProdSubtype (D.f j i) (D.f j k)).inv ‚ü®‚ü®_, _‚ü©, e‚ÇÇ.trans e‚ÇÉ.symm‚ü©
    have eq‚ÇÅ : (D.t j i) ((pullback.fst _ _ : _ /-(D.f j k)-/ ‚ü∂ D.V (j, i)) z) = x := by
      dsimp only [coe_of, z]
      rw [pullbackIsoProdSubtype_inv_fst_apply, D.t_inv_apply]
    have eq‚ÇÇ : (pullback.snd _ _ : _ ‚ü∂ D.V _) z = y := pullbackIsoProdSubtype_inv_snd_apply _ _ _
    clear_value z
    use (pullback.fst _ _ : _ ‚ü∂ D.V (i, k)) (D.t' _ _ _ z)
    dsimp only at *
    substs eq‚ÇÅ eq‚ÇÇ e‚ÇÅ e‚ÇÉ e‚ÇÑ
    have h‚ÇÅ : D.t' j i k ‚â´ pullback.fst _ _ ‚â´ D.f i k = pullback.fst _ _ ‚â´ D.t j i ‚â´ D.f i j := by
      rw [‚Üê ùñ£.t_fac_assoc]; congr 1; exact pullback.condition
    have h‚ÇÇ : D.t' j i k ‚â´ pullback.fst _ _ ‚â´ D.t i k ‚â´ D.f k i =
        pullback.snd _ _ ‚â´ D.t j k ‚â´ D.f k j := by
      rw [‚Üê ùñ£.t_fac_assoc]
      apply @Epi.left_cancellation _ _ _ _ (D.t' k j i)
      rw [ùñ£.cocycle_assoc, ùñ£.t_fac_assoc, ùñ£.t_inv_assoc]
      exact pullback.condition.symm
    exact ‚ü®CategoryTheory.congr_fun h‚ÇÅ z, CategoryTheory.congr_fun h‚ÇÇ z‚ü©‚ü©

open CategoryTheory.Limits.WalkingParallelPair

theorem eqvGen_of_œÄ_eq
    -- Porting note: was `{x y : ‚àê D.U}`
    {x y : sigmaObj (Œ≤ := D.toGlueData.J) (C := TopCat) D.toGlueData.U}
    (h : ùñ£.œÄ x = ùñ£.œÄ y) :
    Relation.EqvGen
      (Function.Coequalizer.Rel ùñ£.diagram.fstSigmaMap ùñ£.diagram.sndSigmaMap) x y := by
  delta GlueData.œÄ Multicoequalizer.sigmaœÄ at h
  replace h : coequalizer.œÄ D.diagram.fstSigmaMap D.diagram.sndSigmaMap x =
      coequalizer.œÄ D.diagram.fstSigmaMap D.diagram.sndSigmaMap y :=
    (TopCat.mono_iff_injective (Multicoequalizer.isoCoequalizer ùñ£.diagram).inv).mp
    inferInstance h
  let diagram := parallelPair ùñ£.diagram.fstSigmaMap ùñ£.diagram.sndSigmaMap ‚ãô forget _
  have : colimit.Œπ diagram one x = colimit.Œπ diagram one y := by
    dsimp only [coequalizer.œÄ] at h
    rw [‚Üê Œπ_preservesColimitIso_hom, ConcreteCategory.forget_map_eq_coe, types_comp_apply]
    simp_all
  have :
    (colimit.Œπ diagram _ ‚â´ colim.map _ ‚â´ (colimit.isoColimitCocone _).hom) _ =
      (colimit.Œπ diagram _ ‚â´ colim.map _ ‚â´ (colimit.isoColimitCocone _).hom) _ :=
    (congr_arg
        (colim.map (diagramIsoParallelPair diagram).hom ‚â´
          (colimit.isoColimitCocone (Types.coequalizerColimit _ _)).hom)
        this :
      _)
  -- Porting note: was
  -- simp only [eqToHom_refl, types_comp_apply, colimit.Œπ_map_assoc,
  --   diagramIsoParallelPair_hom_app, colimit.isoColimitCocone_Œπ_hom, types_id_apply] at this
  -- See https://github.com/leanprover-community/mathlib4/issues/5026
  rw [colimit.Œπ_map_assoc, diagramIsoParallelPair_hom_app, eqToHom_refl,
    colimit.isoColimitCocone_Œπ_hom, types_comp_apply, types_id_apply, types_comp_apply,
    types_id_apply] at this
  exact Quot.eq.1 this

theorem Œπ_eq_iff_rel (i j : D.J) (x : D.U i) (y : D.U j) :
    ùñ£.Œπ i x = ùñ£.Œπ j y ‚Üî D.Rel ‚ü®i, x‚ü© ‚ü®j, y‚ü© := by
  constructor
  ¬∑ delta GlueData.Œπ
    simp_rw [‚Üê Multicoequalizer.Œπ_sigmaœÄ]
    intro h
    rw [‚Üê
      show _ = Sigma.mk i x from ConcreteCategory.congr_hom (sigmaIsoSigma.{_, u} D.U).inv_hom_id _]
    rw [‚Üê
      show _ = Sigma.mk j y from ConcreteCategory.congr_hom (sigmaIsoSigma.{_, u} D.U).inv_hom_id _]
    change InvImage D.Rel (sigmaIsoSigma.{_, u} D.U).hom _ _
    rw [‚Üê (InvImage.equivalence _ _ D.rel_equiv).eqvGen_iff]
    refine Relation.EqvGen.mono ?_ (D.eqvGen_of_œÄ_eq h :)
    rintro _ _ ‚ü®x‚ü©
    obtain ‚ü®‚ü®‚ü®i, j‚ü©, y‚ü©, rfl‚ü© :=
      (ConcreteCategory.bijective_of_isIso (sigmaIsoSigma.{u, u} _).inv).2 x
    unfold InvImage MultispanIndex.fstSigmaMap MultispanIndex.sndSigmaMap
    rw [sigmaIsoSigma_inv_apply]
    -- `rw [‚Üê ConcreteCategory.comp_apply]` succeeds but rewrites the wrong expression
    erw [‚Üê ConcreteCategory.comp_apply, ‚Üê ConcreteCategory.comp_apply, colimit.Œπ_desc_assoc,
      ‚Üê ConcreteCategory.comp_apply, ‚Üê ConcreteCategory.comp_apply, colimit.Œπ_desc_assoc]
      -- previous line now `erw` after https://github.com/leanprover-community/mathlib4/pull/13170
    erw [sigmaIsoSigma_hom_Œπ_apply, sigmaIsoSigma_hom_Œπ_apply]
    exact ‚ü®y, ‚ü®rfl, rfl‚ü©‚ü©
  ¬∑ rintro ‚ü®z, e‚ÇÅ, e‚ÇÇ‚ü©
    dsimp only at *
    -- Porting note: there were `subst e‚ÇÅ` and `subst e‚ÇÇ`, instead of the `rw`
    rw [‚Üê e‚ÇÅ, ‚Üê e‚ÇÇ] at *
    rw [D.glue_condition_apply]

theorem Œπ_injective (i : D.J) : Function.Injective (ùñ£.Œπ i) := by
  intro x y h
  rcases (D.Œπ_eq_iff_rel _ _ _ _).mp h with ‚ü®_, e‚ÇÅ, e‚ÇÇ‚ü©
  ¬∑ dsimp only at *
    -- Porting note: there were `cases e‚ÇÅ` and `cases e‚ÇÇ`, instead of the `rw`
    rw [‚Üê e‚ÇÅ, ‚Üê e‚ÇÇ]
    simp

instance Œπ_mono (i : D.J) : Mono (ùñ£.Œπ i) :=
  (TopCat.mono_iff_injective _).mpr (D.Œπ_injective _)

theorem image_inter (i j : D.J) :
    Set.range (ùñ£.Œπ i) ‚à© Set.range (ùñ£.Œπ j) = Set.range (D.f i j ‚â´ ùñ£.Œπ _) := by
  ext x
  constructor
  ¬∑ rintro ‚ü®‚ü®x‚ÇÅ, eq‚ÇÅ‚ü©, ‚ü®x‚ÇÇ, eq‚ÇÇ‚ü©‚ü©
    obtain ‚ü®y, e‚ÇÅ, -‚ü© := (D.Œπ_eq_iff_rel _ _ _ _).mp (eq‚ÇÅ.trans eq‚ÇÇ.symm)
    ¬∑ substs eq‚ÇÅ
      exact ‚ü®y, by simp [e‚ÇÅ]‚ü©
  ¬∑ rintro ‚ü®x, hx‚ü©
    refine ‚ü®‚ü®D.f i j x, hx‚ü©, ‚ü®D.f j i (D.t _ _ x), ?_‚ü©‚ü©
    rw [D.glue_condition_apply]
    exact hx

theorem preimage_range (i j : D.J) : ùñ£.Œπ j ‚Åª¬π' Set.range (ùñ£.Œπ i) = Set.range (D.f j i) := by
  rw [‚Üê Set.preimage_image_eq (Set.range (D.f j i)) (D.Œπ_injective j), ‚Üê Set.image_univ, ‚Üê
    Set.image_univ, ‚Üê Set.image_comp, ‚Üê coe_comp, Set.image_univ, Set.image_univ, ‚Üê image_inter,
    Set.preimage_range_inter]

theorem preimage_image_eq_image (i j : D.J) (U : Set (ùñ£.U i)) :
    ùñ£.Œπ j ‚Åª¬π' (ùñ£.Œπ i '' U) = D.f _ _ '' ((D.t j i ‚â´ D.f _ _) ‚Åª¬π' U) := by
  have : D.f _ _ ‚Åª¬π' (ùñ£.Œπ j ‚Åª¬π' (ùñ£.Œπ i '' U)) = (D.t j i ‚â´ D.f _ _) ‚Åª¬π' U := by
    ext x
    conv_rhs => rw [‚Üê Set.preimage_image_eq U (D.Œπ_injective _)]
    simp
  rw [‚Üê this, Set.image_preimage_eq_inter_range]
  symm
  apply Set.inter_eq_self_of_subset_left
  rw [‚Üê D.preimage_range i j]
  exact Set.preimage_mono (Set.image_subset_range _ _)

theorem preimage_image_eq_image' (i j : D.J) (U : Set (ùñ£.U i)) :
    ùñ£.Œπ j ‚Åª¬π' (ùñ£.Œπ i '' U) = (D.t i j ‚â´ D.f _ _) '' (D.f _ _ ‚Åª¬π' U) := by
  convert D.preimage_image_eq_image i j U using 1
  rw [coe_comp, coe_comp, Set.image_comp]
  congr! 1
  rw [‚Üê Set.eq_preimage_iff_image_eq, Set.preimage_preimage]
  ¬∑ change _ = (D.t i j ‚â´ D.t j i ‚â´ _) ‚Åª¬π' _
    rw [ùñ£.t_inv_assoc]
  rw [‚Üê isIso_iff_bijective]
  apply (forget TopCat).map_isIso

theorem open_image_open (i : D.J) (U : Opens (ùñ£.U i)) : IsOpen (ùñ£.Œπ i '' U) := by
  rw [isOpen_iff]
  intro j
  rw [preimage_image_eq_image]
  apply (D.f_open _ _).isOpenMap
  apply (D.t j i ‚â´ D.f i j).hom.continuous_toFun.isOpen_preimage
  exact U.isOpen

theorem Œπ_isOpenEmbedding (i : D.J) : IsOpenEmbedding (ùñ£.Œπ i) :=
  .of_continuous_injective_isOpenMap (ùñ£.Œπ i).hom.continuous_toFun (D.Œπ_injective i) fun U h =>
    D.open_image_open i ‚ü®U, h‚ü©

/-- A family of gluing data consists of
1. An index type `J`
2. A bundled topological space `U i` for each `i : J`.
3. An open set `V i j ‚äÜ U i` for each `i j : J`.
4. A transition map `t i j : V i j ‚ü∂ V j i` for each `i j : Œπ`.
such that
6. `V i i = U i`.
7. `t i i` is the identity.
8. For each `x ‚àà V i j ‚à© V i k`, `t i j x ‚àà V j k`.
9. `t j k (t i j x) = t i k x`.

We can then glue the topological spaces `U i` together by identifying `V i j` with `V j i`.
-/
structure MkCore where
  /-- The index type `J` -/
  {J : Type u}
  /-- For each `i : J`, a bundled topological space `U i` -/
  U : J ‚Üí TopCat.{u}
  /-- For each `i j : J`, an open set `V i j ‚äÜ U i` -/
  V : ‚àÄ i, J ‚Üí Opens (U i)
  /-- For each `i j : Œπ`, a transition map `t i j : V i j ‚ü∂ V j i` -/
  t : ‚àÄ i j, (Opens.toTopCat _).obj (V i j) ‚ü∂ (Opens.toTopCat _).obj (V j i)
  V_id : ‚àÄ i, V i i = ‚ä§
  t_id : ‚àÄ i, ‚áë(t i i) = id
  t_inter : ‚àÄ ‚¶Éi j‚¶Ñ (k) (x : V i j), ‚Üëx ‚àà V i k ‚Üí (((‚Üë) : (V j i) ‚Üí (U j)) (t i j x)) ‚àà V j k
  cocycle :
    ‚àÄ (i j k) (x : V i j) (h : ‚Üëx ‚àà V i k),
      -- Porting note: the underscore in the next line was `‚Üë(t i j x)`, but Lean type-mismatched
      (((‚Üë) : (V k j) ‚Üí (U k)) (t j k ‚ü®_, t_inter k x h‚ü©)) = ((‚Üë) : (V k i) ‚Üí (U k)) (t i k ‚ü®x, h‚ü©)

theorem MkCore.t_inv (h : MkCore) (i j : h.J) (x : h.V j i) : h.t i j ((h.t j i) x) = x := by
  have := h.cocycle j i j x ?_
  ¬∑ rw [h.t_id] at this
    ¬∑ convert Subtype.eq this
  rw [h.V_id]
  trivial

instance (h : MkCore.{u}) (i j : h.J) : IsIso (h.t i j) := by
  use h.t j i; constructor <;> ext1; exacts [h.t_inv _ _ _, h.t_inv _ _ _]

/-- (Implementation) the restricted transition map to be fed into `TopCat.GlueData`. -/
def MkCore.t' (h : MkCore.{u}) (i j k : h.J) :
    pullback (h.V i j).inclusion' (h.V i k).inclusion' ‚ü∂
      pullback (h.V j k).inclusion' (h.V j i).inclusion' := by
  refine (pullbackIsoProdSubtype _ _).hom ‚â´ ofHom ‚ü®?_, ?_‚ü© ‚â´ (pullbackIsoProdSubtype _ _).inv
  ¬∑ intro x
    refine ‚ü®‚ü®‚ü®(h.t i j x.1.1).1, ?_‚ü©, h.t i j x.1.1‚ü©, rfl‚ü©
    rcases x with ‚ü®‚ü®‚ü®x, hx‚ü©, ‚ü®x', hx'‚ü©‚ü©, rfl : x = x'‚ü©
    exact h.t_inter _ ‚ü®x, hx‚ü© hx'
  fun_prop

/-- This is a constructor of `TopCat.GlueData` whose arguments are in terms of elements and
intersections rather than subobjects and pullbacks. Please refer to `TopCat.GlueData.MkCore` for
details. -/
def mk' (h : MkCore.{u}) : TopCat.GlueData where
  J := h.J
  U := h.U
  V i := (Opens.toTopCat _).obj (h.V i.1 i.2)
  f i j := (h.V i j).inclusion'
  f_id i := (h.V_id i).symm ‚ñ∏ (Opens.inclusionTopIso (h.U i)).isIso_hom
  f_open := fun i j : h.J => (h.V i j).isOpenEmbedding
  t := h.t
  t_id i := by ext; rw [h.t_id]; rfl
  t' := h.t'
  t_fac i j k := by
    delta MkCore.t'
    rw [Category.assoc, Category.assoc, pullbackIsoProdSubtype_inv_snd, ‚Üê Iso.eq_inv_comp,
      pullbackIsoProdSubtype_inv_fst_assoc]
    ext ‚ü®‚ü®‚ü®x, hx‚ü©, ‚ü®x', hx'‚ü©‚ü©, rfl : x = x'‚ü©
    rfl
  cocycle i j k := by
    delta MkCore.t'
    simp_rw [‚Üê Category.assoc]
    rw [Iso.comp_inv_eq]
    simp only [Iso.inv_hom_id_assoc, Category.assoc, Category.id_comp]
    rw [‚Üê Iso.eq_inv_comp, Iso.inv_hom_id]
    ext1 ‚ü®‚ü®‚ü®x, hx‚ü©, ‚ü®x', hx'‚ü©‚ü©, rfl : x = x'‚ü©
    dsimp only [Opens.coe_inclusion', hom_comp, hom_ofHom, ContinuousMap.comp_assoc,
      ContinuousMap.comp_apply, ContinuousMap.coe_mk, hom_id, ContinuousMap.id_apply]
    rw [Subtype.mk_eq_mk, Prod.mk_inj, Subtype.mk_eq_mk, Subtype.ext_iff, and_self_iff]
    convert congr_arg Subtype.val (h.t_inv k i ‚ü®x, hx'‚ü©) using 3
    refine Subtype.ext ?_
    exact h.cocycle i j k ‚ü®x, hx‚ü© hx'
  f_mono _ _ := (TopCat.mono_iff_injective _).mpr fun _ _ h => Subtype.ext h

variable {Œ± : Type u} [TopologicalSpace Œ±] {J : Type u} (U : J ‚Üí Opens Œ±)

/-- We may construct a glue data from a family of open sets. -/
@[simps! toGlueData_J toGlueData_U toGlueData_V toGlueData_t toGlueData_f]
def ofOpenSubsets : TopCat.GlueData.{u} :=
  mk'.{u}
    { J
      U := fun i => (Opens.toTopCat <| TopCat.of Œ±).obj (U i)
      V := fun _ j => (Opens.map <| Opens.inclusion' _).obj (U j)
      t := fun i j => ofHom ‚ü®fun x => ‚ü®‚ü®x.1.1, x.2‚ü©, x.1.2‚ü©, by fun_prop‚ü©
      V_id := fun i => by ext; simp
      t_id := fun i => by ext; rfl
      t_inter := fun _ _ _ _ hx => hx
      cocycle := fun _ _ _ _ _ => rfl }

/-- The canonical map from the glue of a family of open subsets `Œ±` into `Œ±`.
This map is an open embedding (`fromOpenSubsetsGlue_isOpenEmbedding`),
and its range is `‚ãÉ i, (U i : Set Œ±)` (`range_fromOpenSubsetsGlue`).
-/
def fromOpenSubsetsGlue : (ofOpenSubsets U).toGlueData.glued ‚ü∂ TopCat.of Œ± :=
  Multicoequalizer.desc _ _ (fun _ => Opens.inclusion' _) (by rintro ‚ü®i, j‚ü©; ext x; rfl)

-- Porting note: `elementwise` here produces a bad lemma,
-- where too much has been simplified, despite the `nosimp`.
@[simp, elementwise nosimp]
theorem Œπ_fromOpenSubsetsGlue (i : J) :
    (ofOpenSubsets U).toGlueData.Œπ i ‚â´ fromOpenSubsetsGlue U = Opens.inclusion' _ :=
  Multicoequalizer.œÄ_desc _ _ _ _ _

theorem fromOpenSubsetsGlue_injective : Function.Injective (fromOpenSubsetsGlue U) := by
  intro x y e
  obtain ‚ü®i, ‚ü®x, hx‚ü©, rfl‚ü© := (ofOpenSubsets U).Œπ_jointly_surjective x
  obtain ‚ü®j, ‚ü®y, hy‚ü©, rfl‚ü© := (ofOpenSubsets U).Œπ_jointly_surjective y
  -- see the porting note on `Œπ_fromOpenSubsetsGlue`
  rw [Œπ_fromOpenSubsetsGlue_apply, Œπ_fromOpenSubsetsGlue_apply] at e
  change x = y at e
  subst e
  rw [(ofOpenSubsets U).Œπ_eq_iff_rel]
  exact ‚ü®‚ü®‚ü®x, hx‚ü©, hy‚ü©, rfl, rfl‚ü©

theorem fromOpenSubsetsGlue_isOpenMap : IsOpenMap (fromOpenSubsetsGlue U) := by
  intro s hs
  rw [(ofOpenSubsets U).isOpen_iff] at hs
  rw [isOpen_iff_forall_mem_open]
  rintro _ ‚ü®x, hx, rfl‚ü©
  obtain ‚ü®i, ‚ü®x, hx'‚ü©, rfl‚ü© := (ofOpenSubsets U).Œπ_jointly_surjective x
  use fromOpenSubsetsGlue U '' s ‚à© Set.range (@Opens.inclusion' (TopCat.of Œ±) (U i))
  use Set.inter_subset_left
  constructor
  ¬∑ rw [‚Üê Set.image_preimage_eq_inter_range]
    apply (Opens.isOpenEmbedding (X := TopCat.of Œ±) (U i)).isOpenMap
    convert hs i using 1
    rw [‚Üê Œπ_fromOpenSubsetsGlue, coe_comp, Set.preimage_comp]
    congr! 1
    exact Set.preimage_image_eq _ (fromOpenSubsetsGlue_injective U)
  ¬∑ refine ‚ü®Set.mem_image_of_mem _ hx, ?_‚ü©
    rw [Œπ_fromOpenSubsetsGlue_apply]
    exact Set.mem_range_self (f := (Opens.inclusion' _).hom) ‚ü®x, hx'‚ü©

theorem fromOpenSubsetsGlue_isOpenEmbedding : IsOpenEmbedding (fromOpenSubsetsGlue U) :=
  .of_continuous_injective_isOpenMap (ContinuousMap.continuous_toFun _)
    (fromOpenSubsetsGlue_injective U) (fromOpenSubsetsGlue_isOpenMap U)

theorem range_fromOpenSubsetsGlue : Set.range (fromOpenSubsetsGlue U) = ‚ãÉ i, (U i : Set Œ±) := by
  ext
  constructor
  ¬∑ rintro ‚ü®x, rfl‚ü©
    obtain ‚ü®i, ‚ü®x, hx'‚ü©, rfl‚ü© := (ofOpenSubsets U).Œπ_jointly_surjective x
    rw [Œπ_fromOpenSubsetsGlue_apply]
    exact Set.subset_iUnion _ i hx'
  ¬∑ rintro ‚ü®_, ‚ü®i, rfl‚ü©, hx‚ü©
    rename_i x
    exact ‚ü®(ofOpenSubsets U).toGlueData.Œπ i ‚ü®x, hx‚ü©, Œπ_fromOpenSubsetsGlue_apply _ _ _‚ü©

/-- The gluing of an open cover is homeomomorphic to the original space. -/
def openCoverGlueHomeo (h : ‚ãÉ i, (U i : Set Œ±) = Set.univ) :
    (ofOpenSubsets U).toGlueData.glued ‚âÉ‚Çú Œ± :=
  Equiv.toHomeomorphOfContinuousOpen
    (Equiv.ofBijective (fromOpenSubsetsGlue U)
      ‚ü®fromOpenSubsetsGlue_injective U,
        Set.range_eq_univ.mp ((range_fromOpenSubsetsGlue U).symm ‚ñ∏ h)‚ü©)
    (fromOpenSubsetsGlue U).hom.2 (fromOpenSubsetsGlue_isOpenMap U)

end GlueData

end TopCat
