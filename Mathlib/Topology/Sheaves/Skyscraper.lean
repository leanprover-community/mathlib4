/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Junyan Xu
-/
import Mathlib.Topology.Sheaves.PUnit
import Mathlib.Topology.Sheaves.Stalks
import Mathlib.Topology.Sheaves.Functors

/-!
# Skyscraper (pre)sheaves

A skyscraper (pre)sheaf `ğ“• : (Pre)Sheaf C X` is the (pre)sheaf with value `A` at point `pâ‚€` that is
supported only at open sets contain `pâ‚€`, i.e. `ğ“•(U) = A` if `pâ‚€ âˆˆ U` and `ğ“•(U) = *` if `pâ‚€ âˆ‰ U`
where `*` is a terminal object of `C`. In terms of stalks, `ğ“•` is supported at all specializations
of `pâ‚€`, i.e. if `pâ‚€ â¤³ x` then `ğ“•â‚“ â‰… A` and if `Â¬ pâ‚€ â¤³ x` then `ğ“•â‚“ â‰… *`.

## Main definitions

* `skyscraperPresheaf`: `skyscraperPresheaf pâ‚€ A` is the skyscraper presheaf at point `pâ‚€` with
  value `A`.
* `skyscraperSheaf`: the skyscraper presheaf satisfies the sheaf condition.

## Main statements

* `skyscraperPresheafStalkOfSpecializes`: if `y âˆˆ closure {pâ‚€}` then the stalk of
  `skyscraperPresheaf pâ‚€ A` at `y` is `A`.
* `skyscraperPresheafStalkOfNotSpecializes`: if `y âˆ‰ closure {pâ‚€}` then the stalk of
  `skyscraperPresheaf pâ‚€ A` at `y` is `*` the terminal object.

TODO: generalize universe level when calculating stalks, after generalizing universe level of stalk.
-/

noncomputable section

open TopologicalSpace TopCat CategoryTheory CategoryTheory.Limits Opposite
open scoped AlgebraicGeometry

universe u v w

variable {X : TopCat.{u}} (pâ‚€ : X) [âˆ€ U : Opens X, Decidable (pâ‚€ âˆˆ U)]

section

variable {C : Type v} [Category.{w} C] [HasTerminal C] (A : C)

/-- A skyscraper presheaf is a presheaf supported at a single point: if `pâ‚€ âˆˆ X` is a specified
point, then the skyscraper presheaf `ğ“•` with value `A` is defined by `U â†¦ A` if `pâ‚€ âˆˆ U` and
`U â†¦ *` if `pâ‚€ âˆ‰ A` where `*` is some terminal object.
-/
@[simps]
def skyscraperPresheaf : Presheaf C X where
  obj U := if pâ‚€ âˆˆ unop U then A else terminal C
  map {U V} i :=
    if h : pâ‚€ âˆˆ unop V then eqToHom <| by rw [if_pos h, if_pos (by simpa using i.unop.le h)]
    else ((if_neg h).symm.ndrec terminalIsTerminal).from _
  map_id U :=
    (em (pâ‚€ âˆˆ U.unop)).elim (fun h => dif_pos h) fun h =>
      ((if_neg h).symm.ndrec terminalIsTerminal).hom_ext _ _
  map_comp {U V W} iVU iWV := by
    by_cases hW : pâ‚€ âˆˆ unop W
    Â· have hV : pâ‚€ âˆˆ unop V := leOfHom iWV.unop hW
      simp only [dif_pos hW, dif_pos hV, eqToHom_trans]
    Â· dsimp; rw [dif_neg hW]; apply ((if_neg hW).symm.ndrec terminalIsTerminal).hom_ext

theorem skyscraperPresheaf_eq_pushforward
    [hd : âˆ€ U : Opens (TopCat.of PUnit.{u + 1}), Decidable (PUnit.unit âˆˆ U)] :
    skyscraperPresheaf pâ‚€ A =
      (ofHom (ContinuousMap.const (TopCat.of PUnit) pâ‚€)) _*
        skyscraperPresheaf (X := TopCat.of PUnit) PUnit.unit A := by
  convert_to @skyscraperPresheaf X pâ‚€ (fun U => hd <| (Opens.map <| ofHom <|
      ContinuousMap.const _ pâ‚€).obj U)
    C _ _ A = _ <;> congr

/-- Taking skyscraper presheaf at a point is functorial: `c â†¦ skyscraper pâ‚€ c` defines a functor by
sending every `f : a âŸ¶ b` to the natural transformation `Î±` defined as: `Î±(U) = f : a âŸ¶ b` if
`pâ‚€ âˆˆ U` and the unique morphism to a terminal object in `C` if `pâ‚€ âˆ‰ U`.
-/
@[simps]
def SkyscraperPresheafFunctor.map' {a b : C} (f : a âŸ¶ b) :
    skyscraperPresheaf pâ‚€ a âŸ¶ skyscraperPresheaf pâ‚€ b where
  app U :=
    if h : pâ‚€ âˆˆ U.unop then eqToHom (if_pos h) â‰« f â‰« eqToHom (if_pos h).symm
    else ((if_neg h).symm.ndrec terminalIsTerminal).from _
  naturality U V i := by
    simp only [skyscraperPresheaf_map]
    by_cases hV : pâ‚€ âˆˆ V.unop
    Â· have hU : pâ‚€ âˆˆ U.unop := leOfHom i.unop hV
      simp only [skyscraperPresheaf_obj, hU, hV, â†“reduceDIte, eqToHom_trans_assoc, Category.assoc,
        eqToHom_trans]
    Â· apply ((if_neg hV).symm.ndrec terminalIsTerminal).hom_ext

theorem SkyscraperPresheafFunctor.map'_id {a : C} :
    SkyscraperPresheafFunctor.map' pâ‚€ (ğŸ™ a) = ğŸ™ _ := by
  ext U
  simp only [SkyscraperPresheafFunctor.map'_app]; split_ifs <;> cat_disch

theorem SkyscraperPresheafFunctor.map'_comp {a b c : C} (f : a âŸ¶ b) (g : b âŸ¶ c) :
    SkyscraperPresheafFunctor.map' pâ‚€ (f â‰« g) =
      SkyscraperPresheafFunctor.map' pâ‚€ f â‰« SkyscraperPresheafFunctor.map' pâ‚€ g := by
  ext U
  simp only [SkyscraperPresheafFunctor.map'_app]
  split_ifs with h <;> cat_disch

/-- Taking skyscraper presheaf at a point is functorial: `c â†¦ skyscraper pâ‚€ c` defines a functor by
sending every `f : a âŸ¶ b` to the natural transformation `Î±` defined as: `Î±(U) = f : a âŸ¶ b` if
`pâ‚€ âˆˆ U` and the unique morphism to a terminal object in `C` if `pâ‚€ âˆ‰ U`.
-/
@[simps]
def skyscraperPresheafFunctor : C â¥¤ Presheaf C X where
  obj := skyscraperPresheaf pâ‚€
  map := SkyscraperPresheafFunctor.map' pâ‚€
  map_id _ := SkyscraperPresheafFunctor.map'_id pâ‚€
  map_comp := SkyscraperPresheafFunctor.map'_comp pâ‚€

end

section

-- In this section, we calculate the stalks for skyscraper presheaves.
-- We need to restrict universe level.
variable {C : Type v} [Category.{u} C] (A : C) [HasTerminal C]

/-- The cocone at `A` for the stalk functor of `skyscraperPresheaf pâ‚€ A` when `y âˆˆ closure {pâ‚€}`
-/
@[simps]
def skyscraperPresheafCoconeOfSpecializes {y : X} (h : pâ‚€ â¤³ y) :
    Cocone ((OpenNhds.inclusion y).op â‹™ skyscraperPresheaf pâ‚€ A) where
  pt := A
  Î¹ :=
    { app := fun U => eqToHom <| if_pos <| h.mem_open U.unop.1.2 U.unop.2
      naturality := fun U V inc => by
        change dite _ _ _ â‰« _ = _; rw [dif_pos]
        swap
        Â· exact h.mem_open V.unop.1.2 V.unop.2
        Â· simp only [Functor.comp_obj, Functor.op_obj, skyscraperPresheaf_obj, unop_op,
            Functor.const_obj_obj, eqToHom_trans, Functor.const_obj_map, Category.comp_id] }

/--
The cocone at `A` for the stalk functor of `skyscraperPresheaf pâ‚€ A` when `y âˆˆ closure {pâ‚€}` is a
colimit
-/
noncomputable def skyscraperPresheafCoconeIsColimitOfSpecializes {y : X} (h : pâ‚€ â¤³ y) :
    IsColimit (skyscraperPresheafCoconeOfSpecializes pâ‚€ A h) where
  desc c := eqToHom (if_pos trivial).symm â‰« c.Î¹.app (op âŠ¤)
  fac c U := by
    dsimp
    rw [â† c.w (homOfLE <| (le_top : unop U â‰¤ _)).op]
    change _ â‰« _ â‰« dite _ _ _ â‰« _ = _
    rw [dif_pos]
    Â· simp only [eqToHom_trans_assoc,
        eqToHom_refl, Category.id_comp, op_unop]
    Â· exact h.mem_open U.unop.1.2 U.unop.2
  uniq c f h := by
    dsimp
    rw [â† h, skyscraperPresheafCoconeOfSpecializes_Î¹_app, eqToHom_trans_assoc, eqToHom_refl,
      Category.id_comp]

/-- If `y âˆˆ closure {pâ‚€}`, then the stalk of `skyscraperPresheaf pâ‚€ A` at `y` is `A`.
-/
noncomputable def skyscraperPresheafStalkOfSpecializes [HasColimits C] {y : X} (h : pâ‚€ â¤³ y) :
    (skyscraperPresheaf pâ‚€ A).stalk y â‰… A :=
  colimit.isoColimitCocone âŸ¨_, skyscraperPresheafCoconeIsColimitOfSpecializes pâ‚€ A hâŸ©

@[reassoc (attr := simp)]
lemma germ_skyscraperPresheafStalkOfSpecializes_hom [HasColimits C] {y : X} (h : pâ‚€ â¤³ y) (U hU) :
    (skyscraperPresheaf pâ‚€ A).germ U y hU â‰«
      (skyscraperPresheafStalkOfSpecializes pâ‚€ A h).hom = eqToHom (if_pos (h.mem_open U.2 hU)) :=
  colimit.isoColimitCocone_Î¹_hom _ _

/-- The cocone at `*` for the stalk functor of `skyscraperPresheaf pâ‚€ A` when `y âˆ‰ closure {pâ‚€}`
-/
@[simps]
def skyscraperPresheafCocone (y : X) :
    Cocone ((OpenNhds.inclusion y).op â‹™ skyscraperPresheaf pâ‚€ A) where
  pt := terminal C
  Î¹ :=
    { app := fun _ => terminal.from _
      naturality := fun _ _ _ => terminalIsTerminal.hom_ext _ _ }

/--
The cocone at `*` for the stalk functor of `skyscraperPresheaf pâ‚€ A` when `y âˆ‰ closure {pâ‚€}` is a
colimit
-/
noncomputable def skyscraperPresheafCoconeIsColimitOfNotSpecializes {y : X} (h : Â¬pâ‚€ â¤³ y) :
    IsColimit (skyscraperPresheafCocone pâ‚€ A y) :=
  let h1 : âˆƒ U : OpenNhds y, pâ‚€ âˆ‰ U.1 :=
    let âŸ¨U, ho, hâ‚€, hyâŸ© := not_specializes_iff_exists_open.mp h
    âŸ¨âŸ¨âŸ¨U, hoâŸ©, hâ‚€âŸ©, hyâŸ©
  { desc := fun c => eqToHom (if_neg h1.choose_spec).symm â‰« c.Î¹.app (op h1.choose)
    fac := fun c U => by
      change _ = c.Î¹.app (op U.unop)
      simp only [â† c.w (homOfLE <| @inf_le_left _ _ h1.choose U.unop).op, â†
        c.w (homOfLE <| @inf_le_right _ _ h1.choose U.unop).op, â† Category.assoc]
      congr 1
      refine ((if_neg ?_).symm.ndrec terminalIsTerminal).hom_ext _ _
      exact fun h => h1.choose_spec h.1
    uniq := fun c f H => by
      dsimp
      rw [â† Category.id_comp f, â† H, â† Category.assoc]
      congr 1; apply terminalIsTerminal.hom_ext }

/-- If `y âˆ‰ closure {pâ‚€}`, then the stalk of `skyscraperPresheaf pâ‚€ A` at `y` is isomorphic to a
terminal object.
-/
noncomputable def skyscraperPresheafStalkOfNotSpecializes [HasColimits C] {y : X} (h : Â¬pâ‚€ â¤³ y) :
    (skyscraperPresheaf pâ‚€ A).stalk y â‰… terminal C :=
  colimit.isoColimitCocone âŸ¨_, skyscraperPresheafCoconeIsColimitOfNotSpecializes _ A hâŸ©

/-- If `y âˆ‰ closure {pâ‚€}`, then the stalk of `skyscraperPresheaf pâ‚€ A` at `y` is a terminal object
-/
def skyscraperPresheafStalkOfNotSpecializesIsTerminal [HasColimits C] {y : X} (h : Â¬pâ‚€ â¤³ y) :
    IsTerminal ((skyscraperPresheaf pâ‚€ A).stalk y) :=
  IsTerminal.ofIso terminalIsTerminal <| (skyscraperPresheafStalkOfNotSpecializes _ _ h).symm

theorem skyscraperPresheaf_isSheaf : (skyscraperPresheaf pâ‚€ A).IsSheaf := by
  classical exact
    (Presheaf.isSheaf_iso_iff (eqToIso <| skyscraperPresheaf_eq_pushforward pâ‚€ A)).mpr <|
      (Sheaf.pushforward_sheaf_of_sheaf _
        (Presheaf.isSheaf_on_punit_of_isTerminal _ (by
          dsimp [skyscraperPresheaf]
          rw [if_neg]
          Â· exact terminalIsTerminal
          Â· #adaptation_note /-- 2024-03-24
            Previously the universe annotation was not needed here. -/
            exact Set.notMem_empty PUnit.unit.{u + 1})))

/--
The skyscraper presheaf supported at `pâ‚€` with value `A` is the sheaf that assigns `A` to all opens
`U` that contain `pâ‚€` and assigns `*` otherwise.
-/
def skyscraperSheaf : Sheaf C X :=
  âŸ¨skyscraperPresheaf pâ‚€ A, skyscraperPresheaf_isSheaf _ _âŸ©

/-- Taking skyscraper sheaf at a point is functorial: `c â†¦ skyscraper pâ‚€ c` defines a functor by
sending every `f : a âŸ¶ b` to the natural transformation `Î±` defined as: `Î±(U) = f : a âŸ¶ b` if
`pâ‚€ âˆˆ U` and the unique morphism to a terminal object in `C` if `pâ‚€ âˆ‰ U`.
-/
def skyscraperSheafFunctor : C â¥¤ Sheaf C X where
  obj c := skyscraperSheaf pâ‚€ c
  map f := Sheaf.Hom.mk <| (skyscraperPresheafFunctor pâ‚€).map f
  map_id _ := Sheaf.Hom.ext <| (skyscraperPresheafFunctor pâ‚€).map_id _
  map_comp _ _ := Sheaf.Hom.ext <| (skyscraperPresheafFunctor pâ‚€).map_comp _ _

namespace StalkSkyscraperPresheafAdjunctionAuxs

variable [HasColimits C]

/-- If `f : ğ“•.stalk pâ‚€ âŸ¶ c`, then a natural transformation `ğ“• âŸ¶ skyscraperPresheaf pâ‚€ c` can be
defined by: `ğ“•.germ pâ‚€ â‰« f : ğ“•(U) âŸ¶ c` if `pâ‚€ âˆˆ U` and the unique morphism to a terminal object
if `pâ‚€ âˆ‰ U`.
-/
@[simps]
def toSkyscraperPresheaf {ğ“• : Presheaf C X} {c : C} (f : ğ“•.stalk pâ‚€ âŸ¶ c) :
    ğ“• âŸ¶ skyscraperPresheaf pâ‚€ c where
  app U :=
    if h : pâ‚€ âˆˆ U.unop then ğ“•.germ _ pâ‚€ h â‰« f â‰« eqToHom (if_pos h).symm
    else ((if_neg h).symm.ndrec terminalIsTerminal).from _
  naturality U V inc := by
    dsimp
    by_cases hV : pâ‚€ âˆˆ V.unop
    Â· have hU : pâ‚€ âˆˆ U.unop := leOfHom inc.unop hV
      split_ifs
      rw [â† Category.assoc, ğ“•.germ_res' inc, Category.assoc, Category.assoc, eqToHom_trans]
    Â· split_ifs
      exact ((if_neg hV).symm.ndrec terminalIsTerminal).hom_ext ..

/-- If `f : ğ“• âŸ¶ skyscraperPresheaf pâ‚€ c` is a natural transformation, then there is a morphism
`ğ“•.stalk pâ‚€ âŸ¶ c` defined as the morphism from colimit to cocone at `c`.
-/
def fromStalk {ğ“• : Presheaf C X} {c : C} (f : ğ“• âŸ¶ skyscraperPresheaf pâ‚€ c) : ğ“•.stalk pâ‚€ âŸ¶ c :=
  let Ï‡ : Cocone ((OpenNhds.inclusion pâ‚€).op â‹™ ğ“•) :=
    Cocone.mk c <|
      { app := fun U => f.app ((OpenNhds.inclusion pâ‚€).op.obj U) â‰« eqToHom (if_pos U.unop.2)
        naturality := fun U V inc => by
          dsimp only [Functor.const_obj_map, Functor.const_obj_obj, Functor.comp_map,
            Functor.comp_obj, Functor.op_obj, skyscraperPresheaf_obj]
          rw [Category.comp_id, â† Category.assoc, comp_eqToHom_iff, Category.assoc,
            eqToHom_trans, f.naturality, skyscraperPresheaf_map]
          have hV : pâ‚€ âˆˆ (OpenNhds.inclusion pâ‚€).obj V.unop := V.unop.2
          simp only [dif_pos hV] }
  colimit.desc _ Ï‡

@[reassoc (attr := simp)]
lemma germ_fromStalk {ğ“• : Presheaf C X} {c : C} (f : ğ“• âŸ¶ skyscraperPresheaf pâ‚€ c) (U) (hU) :
    ğ“•.germ U pâ‚€ hU â‰« fromStalk pâ‚€ f = f.app (op U) â‰« eqToHom (if_pos hU) :=
  colimit.Î¹_desc _ _

theorem to_skyscraper_fromStalk {ğ“• : Presheaf C X} {c : C} (f : ğ“• âŸ¶ skyscraperPresheaf pâ‚€ c) :
    toSkyscraperPresheaf pâ‚€ (fromStalk _ f) = f := by
  apply NatTrans.ext
  ext U
  dsimp
  split_ifs with h
  Â· rw [â† Category.assoc, germ_fromStalk, Category.assoc, eqToHom_trans, eqToHom_refl,
      Category.comp_id]
  Â· exact ((if_neg h).symm.ndrec terminalIsTerminal).hom_ext ..

theorem fromStalk_to_skyscraper {ğ“• : Presheaf C X} {c : C} (f : ğ“•.stalk pâ‚€ âŸ¶ c) :
    fromStalk pâ‚€ (toSkyscraperPresheaf _ f) = f := by
  refine ğ“•.stalk_hom_ext fun U hxU â†¦ ?_
  rw [germ_fromStalk, toSkyscraperPresheaf_app, dif_pos hxU, Category.assoc, Category.assoc,
    eqToHom_trans, eqToHom_refl, Category.comp_id, Presheaf.germ]

/-- The unit in `Presheaf.stalkFunctor âŠ£ skyscraperPresheafFunctor`
-/
@[simps]
protected def unit :
    ğŸ­ (Presheaf C X) âŸ¶ Presheaf.stalkFunctor C pâ‚€ â‹™ skyscraperPresheafFunctor pâ‚€ where
  app _ := toSkyscraperPresheaf _ <| ğŸ™ _
  naturality ğ“• ğ“– f := by
    ext U; dsimp
    split_ifs with h
    Â· simp only [Category.id_comp, Category.assoc, eqToHom_trans_assoc, eqToHom_refl,
        Presheaf.stalkFunctor_map_germ_assoc, Presheaf.stalkFunctor_obj]
    Â· apply ((if_neg h).symm.ndrec terminalIsTerminal).hom_ext

/-- The counit in `Presheaf.stalkFunctor âŠ£ skyscraperPresheafFunctor`
-/
@[simps]
protected def counit :
    skyscraperPresheafFunctor pâ‚€ â‹™ (Presheaf.stalkFunctor C pâ‚€ : Presheaf C X â¥¤ C) âŸ¶ ğŸ­ C where
  app c := (skyscraperPresheafStalkOfSpecializes pâ‚€ c specializes_rfl).hom
  naturality x y f := TopCat.Presheaf.stalk_hom_ext _ fun U hxU â†¦ by simp [hxU]

end StalkSkyscraperPresheafAdjunctionAuxs

section

open StalkSkyscraperPresheafAdjunctionAuxs

/-- `skyscraperPresheafFunctor` is the right adjoint of `Presheaf.stalkFunctor`
-/
def skyscraperPresheafStalkAdjunction [HasColimits C] :
    (Presheaf.stalkFunctor C pâ‚€ : Presheaf C X â¥¤ C) âŠ£ skyscraperPresheafFunctor pâ‚€ where
  unit := StalkSkyscraperPresheafAdjunctionAuxs.unit _
  counit := StalkSkyscraperPresheafAdjunctionAuxs.counit _
  left_triangle_components X := by
    dsimp [Presheaf.stalkFunctor, toSkyscraperPresheaf]
    ext
    simp only [Functor.comp_obj, Functor.op_obj, Î¹_colimMap_assoc, skyscraperPresheaf_obj,
      Functor.whiskerLeft_app, Category.comp_id]
    split_ifs with h
    Â· simp [skyscraperPresheafStalkOfSpecializes]
      rfl
    Â· simp only [skyscraperPresheafStalkOfSpecializes, colimit.isoColimitCocone_Î¹_hom,
        skyscraperPresheafCoconeOfSpecializes_pt, skyscraperPresheafCoconeOfSpecializes_Î¹_app,
        Functor.comp_obj, Functor.op_obj, skyscraperPresheaf_obj, Functor.const_obj_obj]
      rw [comp_eqToHom_iff]
      apply ((if_neg h).symm.ndrec terminalIsTerminal).hom_ext
  right_triangle_components Y := by
    ext
    simp only [skyscraperPresheafFunctor_obj, Functor.id_obj, skyscraperPresheaf_obj,
      Functor.comp_obj, Presheaf.stalkFunctor_obj, unit_app, counit_app,
      skyscraperPresheafStalkOfSpecializes, skyscraperPresheafFunctor_map, Presheaf.comp_app,
      toSkyscraperPresheaf_app, Category.id_comp, SkyscraperPresheafFunctor.map'_app]
    split_ifs with h
    Â· simp [Presheaf.germ]
      rfl
    Â· simp
      rfl

instance [HasColimits C] : (skyscraperPresheafFunctor pâ‚€ : C â¥¤ Presheaf C X).IsRightAdjoint :=
  (skyscraperPresheafStalkAdjunction _).isRightAdjoint

instance [HasColimits C] : (Presheaf.stalkFunctor C pâ‚€).IsLeftAdjoint :=
  -- Use a classical instance instead of the one from `variable`s
  have : âˆ€ U : Opens X, Decidable (pâ‚€ âˆˆ U) := fun _ â†¦ Classical.dec _
  (skyscraperPresheafStalkAdjunction _).isLeftAdjoint

/-- Taking stalks of a sheaf is the left adjoint functor to `skyscraperSheafFunctor`
-/
def stalkSkyscraperSheafAdjunction [HasColimits C] :
    Sheaf.forget C X â‹™ Presheaf.stalkFunctor _ pâ‚€ âŠ£ skyscraperSheafFunctor pâ‚€ where
  -- Porting note (https://github.com/leanprover-community/mathlib4/issues/11041): `ext1` is changed to `Sheaf.Hom.ext`,
  unit :=
    { app := fun ğ“• => âŸ¨(StalkSkyscraperPresheafAdjunctionAuxs.unit pâ‚€).app ğ“•.1âŸ©
      naturality := fun ğ“ ğ“‘ f => Sheaf.Hom.ext <| by
        apply (StalkSkyscraperPresheafAdjunctionAuxs.unit pâ‚€).naturality }
  counit := StalkSkyscraperPresheafAdjunctionAuxs.counit pâ‚€
  left_triangle_components X :=
    ((skyscraperPresheafStalkAdjunction pâ‚€).left_triangle_components X.val)
  right_triangle_components _ :=
    Sheaf.Hom.ext ((skyscraperPresheafStalkAdjunction pâ‚€).right_triangle_components _)

instance [HasColimits C] : (skyscraperSheafFunctor pâ‚€ : C â¥¤ Sheaf C X).IsRightAdjoint :=
  (stalkSkyscraperSheafAdjunction _).isRightAdjoint

end

end
