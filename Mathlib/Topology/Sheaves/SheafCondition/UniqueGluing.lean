/-
Copyright (c) 2021 Justus Springer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Justus Springer
-/
import Mathlib.Topology.Sheaves.Forget
import Mathlib.CategoryTheory.Limits.Shapes.Types
import Mathlib.Topology.Sheaves.Sheaf
import Mathlib.CategoryTheory.Types

#align_import topology.sheaves.sheaf_condition.unique_gluing from "leanprover-community/mathlib"@"5dc6092d09e5e489106865241986f7f2ad28d4c8"

/-!
# The sheaf condition in terms of unique gluings

We provide an alternative formulation of the sheaf condition in terms of unique gluings.

We work with sheaves valued in a concrete category `C` admitting all limits, whose forgetful
functor `C ‚•§ Type` preserves limits and reflects isomorphisms. The usual categories of algebraic
structures, such as `MonCat`, `AddCommGroupCat`, `RingCat`, `CommRingCat` etc. are all examples of
this kind of category.

A presheaf `F : presheaf C X` satisfies the sheaf condition if and only if, for every
compatible family of sections `sf : Œ† i : Œπ, F.obj (op (U i))`, there exists a unique gluing
`s : F.obj (op (supr U))`.

Here, the family `sf` is called compatible, if for all `i j : Œπ`, the restrictions of `sf i`
and `sf j` to `U i ‚äì U j` agree. A section `s : F.obj (op (supr U))` is a gluing for the
family `sf`, if `s` restricts to `sf i` on `U i` for all `i : Œπ`

We show that the sheaf condition in terms of unique gluings is equivalent to the definition
in terms of equalizers. Our approach is as follows: First, we show them to be equivalent for
`Type`-valued presheaves. Then we use that composing a presheaf with a limit-preserving and
isomorphism-reflecting functor leaves the sheaf condition invariant, as shown in
`Mathlib/Topology/Sheaves/Forget.lean`.

-/

noncomputable section

open TopCat TopCat.Presheaf TopCat.Presheaf.SheafConditionEqualizerProducts CategoryTheory
  CategoryTheory.Limits TopologicalSpace TopologicalSpace.Opens Opposite

universe v u x

variable {C : Type u} [Category.{v} C] [ConcreteCategory.{v} C]

namespace TopCat

namespace Presheaf

section

attribute [local instance] ConcreteCategory.hasCoeToSort ConcreteCategory.funLike

variable {X : TopCat.{x}} (F : Presheaf C X) {Œπ : Type x} (U : Œπ ‚Üí Opens X)

/-- A family of sections `sf` is compatible, if the restrictions of `sf i` and `sf j` to `U i ‚äì U j`
agree, for all `i` and `j`
-/
def IsCompatible (sf : ‚àÄ i : Œπ, F.obj (op (U i))) : Prop :=
  ‚àÄ i j : Œπ, F.map (infLELeft (U i) (U j)).op (sf i) = F.map (infLERight (U i) (U j)).op (sf j)
set_option linter.uppercaseLean3 false in
#align Top.presheaf.is_compatible TopCat.Presheaf.IsCompatible

/-- A section `s` is a gluing for a family of sections `sf` if it restricts to `sf i` on `U i`,
for all `i`
-/
def IsGluing (sf : ‚àÄ i : Œπ, F.obj (op (U i))) (s : F.obj (op (iSup U))) : Prop :=
  ‚àÄ i : Œπ, F.map (Opens.leSupr U i).op s = sf i
set_option linter.uppercaseLean3 false in
#align Top.presheaf.is_gluing TopCat.Presheaf.IsGluing

/--
The sheaf condition in terms of unique gluings. A presheaf `F : presheaf C X` satisfies this sheaf
condition if and only if, for every compatible family of sections `sf : Œ† i : Œπ, F.obj (op (U i))`,
there exists a unique gluing `s : F.obj (op (supr U))`.

We prove this to be equivalent to the usual one below in
`TopCat.Presheaf.isSheaf_iff_isSheafUniqueGluing`
-/
def IsSheafUniqueGluing : Prop :=
  ‚àÄ ‚¶ÉŒπ : Type x‚¶Ñ (U : Œπ ‚Üí Opens X) (sf : ‚àÄ i : Œπ, F.obj (op (U i))),
    IsCompatible F U sf ‚Üí ‚àÉ! s : F.obj (op (iSup U)), IsGluing F U sf s
set_option linter.uppercaseLean3 false in
#align Top.presheaf.is_sheaf_unique_gluing TopCat.Presheaf.IsSheafUniqueGluing

end

section TypeValued

variable {X : TopCat.{x}} (F : Presheaf (Type u) X) {Œπ : Type x} (U : Œπ ‚Üí Opens X) [UnivLE.{x, u}]

/-- For presheaves of types, terms of `piOpens F U` are just families of sections.
-/
def piOpensIsoSectionsFamily : piOpens F U ‚âÉ ‚àÄ i : Œπ, F.obj (op (U i)) :=
  (Types.UnivLE.productIso _).toEquiv.trans (equivShrink _).symm
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pi_opens_iso_sections_family TopCat.Presheaf.piOpensIsoSectionsFamily

@[simp]
theorem piOpensIsoSectionsFamily_apply (sf : piOpens F U) (i : Œπ) :
    piOpensIsoSectionsFamily F U sf i = Pi.œÄ (fun i => F.obj (op (U i))) i sf := by
  simp [piOpensIsoSectionsFamily]
  -- üéâ no goals

/-- Under the isomorphism `piOpensIsoSectionsFamily`, compatibility of sections is the same
as being equalized by the arrows `leftRes` and `rightRes` of the equalizer diagram.
-/
theorem compatible_iff_leftRes_eq_rightRes (sf : piOpens F U) :
    IsCompatible F U (piOpensIsoSectionsFamily F U sf) ‚Üî
    leftRes F U sf = rightRes F U sf := by
  constructor <;> intro h
  -- ‚ä¢ IsCompatible F U (‚Üë(piOpensIsoSectionsFamily F U) sf) ‚Üí leftRes F U sf = rig ‚Ä¶
                  -- ‚ä¢ leftRes F U sf = rightRes F U sf
                  -- ‚ä¢ IsCompatible F U (‚Üë(piOpensIsoSectionsFamily F U) sf)
  ¬∑ -- Porting note : Lean can't use `Types.limit_ext'` as an `ext` lemma
    refine Types.limit_ext _ _ _ fun ‚ü®i, j‚ü© => ?_
    -- ‚ä¢ limit.œÄ (Discrete.functor fun p => F.obj (op (U p.fst ‚äì U p.snd))) { as := ( ‚Ä¶
    rw [leftRes, Types.Limit.lift_œÄ_apply, Fan.mk_œÄ_app, rightRes, Types.Limit.lift_œÄ_apply,
      Fan.mk_œÄ_app]
    simpa using h i j
    -- üéâ no goals
  ¬∑ intro i j
    -- ‚ä¢ ‚Üë(F.map (infLELeft (U i) (U j)).op) (‚Üë(piOpensIsoSectionsFamily F U) sf i) = ‚Ä¶
    convert congr_arg (Limits.Pi.œÄ (fun p : Œπ √ó Œπ => F.obj (op (U p.1 ‚äì U p.2))) (i, j)) h
    -- ‚ä¢ ‚Üë(F.map (infLELeft (U i) (U j)).op) (‚Üë(piOpensIsoSectionsFamily F U) sf i) = ‚Ä¶
    ¬∑ rw [leftRes, Types.pi_lift_œÄ_apply, piOpensIsoSectionsFamily_apply]
      -- ‚ä¢ ‚Üë(F.map (infLELeft (U i) (U j)).op) (Pi.œÄ (fun i => F.obj (op (U i))) i sf)  ‚Ä¶
      rfl
      -- üéâ no goals
    ¬∑ rw [rightRes, Types.pi_lift_œÄ_apply]
      -- ‚ä¢ ‚Üë(F.map (infLERight (U i) (U j)).op) (‚Üë(piOpensIsoSectionsFamily F U) sf j)  ‚Ä¶
      simp only [piOpensIsoSectionsFamily_apply]
      -- ‚ä¢ ‚Üë(F.map (infLERight (U i) (U j)).op) (Pi.œÄ (fun i => F.obj (op (U i))) j sf) ‚Ä¶
      rfl
      -- üéâ no goals
set_option linter.uppercaseLean3 false in
#align Top.presheaf.compatible_iff_left_res_eq_right_res TopCat.Presheaf.compatible_iff_leftRes_eq_rightRes

/-- Under the isomorphism `piOpensIsoSectionsFamily`, being a gluing of a family of
sections `sf` is the same as lying in the preimage of `res` (the leftmost arrow of the
equalizer diagram).
-/
@[simp]
theorem isGluing_iff_eq_res (sf : piOpens F U) (s : F.obj (op (iSup U))) :
    IsGluing F U (piOpensIsoSectionsFamily F U sf) s ‚Üî res F U s = sf := by
  constructor <;> intro h
  -- ‚ä¢ IsGluing F U (‚Üë(piOpensIsoSectionsFamily F U) sf) s ‚Üí res F U s = sf
                  -- ‚ä¢ res F U s = sf
                  -- ‚ä¢ IsGluing F U (‚Üë(piOpensIsoSectionsFamily F U) sf) s
  ¬∑ -- Porting note : Lean can't use `Types.limit_ext'` as an `ext` lemma
    refine Types.limit_ext _ _ _ fun ‚ü®i‚ü© => ?_
    -- ‚ä¢ limit.œÄ (Discrete.functor fun i => F.obj (op (U i))) { as := i } (res F U s) ‚Ä¶
    rw [res, Types.Limit.lift_œÄ_apply, Fan.mk_œÄ_app]
    -- ‚ä¢ F.map (leSupr U { as := i }.as).op s = limit.œÄ (Discrete.functor fun i => F. ‚Ä¶
    simpa using h i
    -- üéâ no goals
  ¬∑ intro i
    -- ‚ä¢ ‚Üë(F.map (leSupr U i).op) s = ‚Üë(piOpensIsoSectionsFamily F U) sf i
    convert congr_arg (Limits.Pi.œÄ (fun i : Œπ => F.obj (op (U i))) i) h
    -- ‚ä¢ ‚Üë(F.map (leSupr U i).op) s = Pi.œÄ (fun i => F.obj (op (U i))) i (res F U s)
    rw [res, Types.pi_lift_œÄ_apply]
    -- ‚ä¢ ‚Üë(F.map (leSupr U i).op) s = F.map (leSupr U i).op s
    ¬∑ rfl
      -- üéâ no goals
    ¬∑ simp
      -- üéâ no goals
set_option linter.uppercaseLean3 false in
#align Top.presheaf.is_gluing_iff_eq_res TopCat.Presheaf.isGluing_iff_eq_res

/-- The "equalizer" sheaf condition can be obtained from the sheaf condition
in terms of unique gluings.
-/
theorem isSheaf_of_isSheafUniqueGluing_types (Fsh : F.IsSheafUniqueGluing) : F.IsSheaf := by
  rw [isSheaf_iff_isSheafEqualizerProducts]
  -- ‚ä¢ IsSheafEqualizerProducts F
  intro Œπ U
  -- ‚ä¢ Nonempty (IsLimit (fork F U))
  refine' ‚ü®Fork.IsLimit.mk' _ _‚ü©
  -- ‚ä¢ (s : Fork (leftRes F U) (rightRes F U)) ‚Üí { l // l ‚â´ Fork.Œπ (fork F U) = For ‚Ä¶
  intro s
  -- ‚ä¢ { l // l ‚â´ Fork.Œπ (fork F U) = Fork.Œπ s ‚àß ‚àÄ {m : ((Functor.const WalkingPara ‚Ä¶
  have h_compatible :
    ‚àÄ x : s.pt, F.IsCompatible U (piOpensIsoSectionsFamily F U (s.Œπ x)) := by
    intro x
    rw [compatible_iff_leftRes_eq_rightRes]
    convert congr_fun s.condition x
  choose m m_spec m_uniq using fun x : s.pt =>
    Fsh U (piOpensIsoSectionsFamily F U (s.Œπ x)) (h_compatible x)
  refine' ‚ü®m, _, _‚ü©
  -- ‚ä¢ m ‚â´ Fork.Œπ (fork F U) = Fork.Œπ s
  ¬∑ -- Porting note : `ext` can't see `limit.hom_ext` applies here:
    -- See https://github.com/leanprover-community/mathlib4/issues/5229
    refine limit.hom_ext fun ‚ü®i‚ü© => funext fun x => ?_
    -- ‚ä¢ ((m ‚â´ Fork.Œπ (fork F U)) ‚â´ limit.œÄ (Discrete.functor fun i => F.obj (op (U i ‚Ä¶
    simp [res]
    -- ‚ä¢ F.map (leSupr U i).op (m x) = limit.œÄ (Discrete.functor fun i => F.obj (op ( ‚Ä¶
    simpa using m_spec x i
    -- üéâ no goals
  ¬∑ intro l hl
    -- ‚ä¢ l = m
    ext x
    -- ‚ä¢ l x = m x
    apply m_uniq
    -- ‚ä¢ IsGluing F U (‚Üë(piOpensIsoSectionsFamily F U) (Fork.Œπ s x)) (l x)
    rw [isGluing_iff_eq_res]
    -- ‚ä¢ res F U (l x) = Fork.Œπ s x
    exact congr_fun hl x
    -- üéâ no goals
set_option linter.uppercaseLean3 false in
#align Top.presheaf.is_sheaf_of_is_sheaf_unique_gluing_types TopCat.Presheaf.isSheaf_of_isSheafUniqueGluing_types

/-- The sheaf condition in terms of unique gluings can be obtained from the usual
"equalizer" sheaf condition.
-/
theorem isSheafUniqueGluing_of_isSheaf_types (Fsh : F.IsSheaf) : F.IsSheafUniqueGluing := by
  rw [isSheaf_iff_isSheafEqualizerProducts] at Fsh
  -- ‚ä¢ IsSheafUniqueGluing F
  intro Œπ U sf hsf
  -- ‚ä¢ ‚àÉ! s, IsGluing F U sf s
  let sf' := (piOpensIsoSectionsFamily F U).symm sf
  -- ‚ä¢ ‚àÉ! s, IsGluing F U sf s
  have hsf' : leftRes F U sf' = rightRes F U sf' := by
    rwa [‚Üê compatible_iff_leftRes_eq_rightRes F U sf', Equiv.apply_symm_apply]
  choose s s_spec s_uniq using Types.unique_of_type_equalizer _ _ (Fsh U).some sf' hsf'
  -- ‚ä¢ ‚àÉ! s, IsGluing F U sf s
  use s
  -- ‚ä¢ (fun s => IsGluing F U sf s) s ‚àß ‚àÄ (y : (forget (Type u)).obj (F.obj (op (iS ‚Ä¶
  dsimp
  -- ‚ä¢ IsGluing F U sf s ‚àß ‚àÄ (y : (forget (Type u)).obj (F.obj (op (iSup U)))), IsG ‚Ä¶
  constructor
  -- ‚ä¢ IsGluing F U sf s
  ¬∑ convert (isGluing_iff_eq_res F U sf' _).mpr s_spec
    -- ‚ä¢ sf = ‚Üë(piOpensIsoSectionsFamily F U) sf'
    simp only [Equiv.apply_symm_apply]
    -- üéâ no goals
  ¬∑ intro y hy
    -- ‚ä¢ y = s
    apply s_uniq
    -- ‚ä¢ res F U y = sf'
    rw [‚Üê isGluing_iff_eq_res F U]
    -- ‚ä¢ IsGluing F U (‚Üë(piOpensIsoSectionsFamily F U) sf') y
    convert hy
    -- ‚ä¢ ‚Üë(piOpensIsoSectionsFamily F U) sf' = sf
    simp only [Equiv.apply_symm_apply]
    -- üéâ no goals
set_option linter.uppercaseLean3 false in
#align Top.presheaf.is_sheaf_unique_gluing_of_is_sheaf_types TopCat.Presheaf.isSheafUniqueGluing_of_isSheaf_types

/-- For type-valued presheaves, the sheaf condition in terms of unique gluings is equivalent to the
usual sheaf condition in terms of equalizer diagrams.
-/
theorem isSheaf_iff_isSheafUniqueGluing_types : F.IsSheaf ‚Üî F.IsSheafUniqueGluing :=
  Iff.intro (isSheafUniqueGluing_of_isSheaf_types F) (isSheaf_of_isSheafUniqueGluing_types F)
set_option linter.uppercaseLean3 false in
#align Top.presheaf.is_sheaf_iff_is_sheaf_unique_gluing_types TopCat.Presheaf.isSheaf_iff_isSheafUniqueGluing_types

end TypeValued

section

attribute [local instance] ConcreteCategory.hasCoeToSort ConcreteCategory.funLike

variable [HasLimits C] [ReflectsIsomorphisms (forget C)] [PreservesLimits (forget C)]

variable {X : TopCat.{v}} (F : Presheaf C X) {Œπ : Type v} (U : Œπ ‚Üí Opens X)

/-- For presheaves valued in a concrete category, whose forgetful functor reflects isomorphisms and
preserves limits, the sheaf condition in terms of unique gluings is equivalent to the usual one
in terms of equalizer diagrams.
-/
theorem isSheaf_iff_isSheafUniqueGluing : F.IsSheaf ‚Üî F.IsSheafUniqueGluing :=
  Iff.trans (isSheaf_iff_isSheaf_comp (forget C) F)
    (isSheaf_iff_isSheafUniqueGluing_types (F ‚ãô forget C))
set_option linter.uppercaseLean3 false in
#align Top.presheaf.is_sheaf_iff_is_sheaf_unique_gluing TopCat.Presheaf.isSheaf_iff_isSheafUniqueGluing

end

end Presheaf

namespace Sheaf

open Presheaf

open CategoryTheory

section

attribute [local instance] ConcreteCategory.hasCoeToSort ConcreteCategory.funLike

variable [HasLimits C] [ReflectsIsomorphisms (ConcreteCategory.forget (C := C))]

variable [PreservesLimits (ConcreteCategory.forget (C := C))]

variable {X : TopCat.{v}} (F : Sheaf C X) {Œπ : Type v} (U : Œπ ‚Üí Opens X)

/-- A more convenient way of obtaining a unique gluing of sections for a sheaf.
-/
theorem existsUnique_gluing (sf : ‚àÄ i : Œπ, F.1.obj (op (U i))) (h : IsCompatible F.1 U sf) :
    ‚àÉ! s : F.1.obj (op (iSup U)), IsGluing F.1 U sf s :=
  (isSheaf_iff_isSheafUniqueGluing F.1).mp F.cond U sf h
set_option linter.uppercaseLean3 false in
#align Top.sheaf.exists_unique_gluing TopCat.Sheaf.existsUnique_gluing

/-- In this version of the lemma, the inclusion homs `iUV` can be specified directly by the user,
which can be more convenient in practice.
-/
theorem existsUnique_gluing' (V : Opens X) (iUV : ‚àÄ i : Œπ, U i ‚ü∂ V) (hcover : V ‚â§ iSup U)
    (sf : ‚àÄ i : Œπ, F.1.obj (op (U i))) (h : IsCompatible F.1 U sf) :
    ‚àÉ! s : F.1.obj (op V), ‚àÄ i : Œπ, F.1.map (iUV i).op s = sf i := by
  have V_eq_supr_U : V = iSup U := le_antisymm hcover (iSup_le fun i => (iUV i).le)
  -- ‚ä¢ ‚àÉ! s, ‚àÄ (i : Œπ), ‚Üë(F.val.map (iUV i).op) s = sf i
  obtain ‚ü®gl, gl_spec, gl_uniq‚ü© := F.existsUnique_gluing U sf h
  -- ‚ä¢ ‚àÉ! s, ‚àÄ (i : Œπ), ‚Üë(F.val.map (iUV i).op) s = sf i
  refine' ‚ü®F.1.map (eqToHom V_eq_supr_U).op gl, _, _‚ü©
  -- ‚ä¢ (fun s => ‚àÄ (i : Œπ), ‚Üë(F.val.map (iUV i).op) s = sf i) (‚Üë(F.val.map (eqToHom ‚Ä¶
  ¬∑ intro i
    -- ‚ä¢ ‚Üë(F.val.map (iUV i).op) (‚Üë(F.val.map (eqToHom V_eq_supr_U).op) gl) = sf i
    rw [‚Üê comp_apply, ‚Üê F.1.map_comp]
    -- ‚ä¢ ‚Üë(F.val.map ((eqToHom V_eq_supr_U).op ‚â´ (iUV i).op)) gl = sf i
    exact gl_spec i
    -- üéâ no goals
  ¬∑ intro gl' gl'_spec
    -- ‚ä¢ gl' = ‚Üë(F.val.map (eqToHom V_eq_supr_U).op) gl
    convert congr_arg _ (gl_uniq (F.1.map (eqToHom V_eq_supr_U.symm).op gl') fun i => _) <;>
    -- ‚ä¢ gl' = ‚Üë(F.val.map (eqToHom V_eq_supr_U).op) (‚Üë(F.val.map (eqToHom (_ : iSup  ‚Ä¶
      rw [‚Üê comp_apply, ‚Üê F.1.map_comp]
      -- ‚ä¢ gl' = ‚Üë(F.val.map ((eqToHom (_ : iSup U = V)).op ‚â´ (eqToHom V_eq_supr_U).op) ‚Ä¶
      -- ‚ä¢ ‚Üë(F.val.map ((eqToHom (_ : iSup U = V)).op ‚â´ (leSupr U i).op)) gl' = sf i
    ¬∑ rw [eqToHom_op, eqToHom_op, eqToHom_trans, eqToHom_refl, F.1.map_id, id_apply]
      -- üéâ no goals
    ¬∑ convert gl'_spec i
      -- üéâ no goals
set_option linter.uppercaseLean3 false in
#align Top.sheaf.exists_unique_gluing' TopCat.Sheaf.existsUnique_gluing'

@[ext]
theorem eq_of_locally_eq (s t : F.1.obj (op (iSup U)))
    (h : ‚àÄ i, F.1.map (Opens.leSupr U i).op s = F.1.map (Opens.leSupr U i).op t) : s = t := by
  let sf : ‚àÄ i : Œπ, F.1.obj (op (U i)) := fun i => F.1.map (Opens.leSupr U i).op s
  -- ‚ä¢ s = t
  have sf_compatible : IsCompatible _ U sf := by
    intro i j
    simp_rw [‚Üê comp_apply, ‚Üê F.1.map_comp]
    rfl
  obtain ‚ü®gl, -, gl_uniq‚ü© := F.existsUnique_gluing U sf sf_compatible
  -- ‚ä¢ s = t
  trans gl
  -- ‚ä¢ s = gl
  ¬∑ apply gl_uniq
    -- ‚ä¢ IsGluing F.val U sf s
    intro i
    -- ‚ä¢ ‚Üë(F.val.map (leSupr U i).op) s = sf i
    rfl
    -- üéâ no goals
  ¬∑ symm
    -- ‚ä¢ t = gl
    apply gl_uniq
    -- ‚ä¢ IsGluing F.val U sf t
    intro i
    -- ‚ä¢ ‚Üë(F.val.map (leSupr U i).op) t = sf i
    rw [‚Üê h]
    -- üéâ no goals
set_option linter.uppercaseLean3 false in
#align Top.sheaf.eq_of_locally_eq TopCat.Sheaf.eq_of_locally_eq

/-- In this version of the lemma, the inclusion homs `iUV` can be specified directly by the user,
which can be more convenient in practice.
-/
theorem eq_of_locally_eq' (V : Opens X) (iUV : ‚àÄ i : Œπ, U i ‚ü∂ V) (hcover : V ‚â§ iSup U)
    (s t : F.1.obj (op V)) (h : ‚àÄ i, F.1.map (iUV i).op s = F.1.map (iUV i).op t) : s = t := by
  have V_eq_supr_U : V = iSup U := le_antisymm hcover (iSup_le fun i => (iUV i).le)
  -- ‚ä¢ s = t
  suffices F.1.map (eqToHom V_eq_supr_U.symm).op s = F.1.map (eqToHom V_eq_supr_U.symm).op t by
    convert congr_arg (F.1.map (eqToHom V_eq_supr_U).op) this <;>
    rw [‚Üê comp_apply, ‚Üê F.1.map_comp, eqToHom_op, eqToHom_op, eqToHom_trans, eqToHom_refl,
      F.1.map_id, id_apply]
  apply eq_of_locally_eq
  -- ‚ä¢ ‚àÄ (i : Œπ), ‚Üë(F.val.map (leSupr U i).op) (‚Üë(F.val.map (eqToHom (_ : iSup U =  ‚Ä¶
  intro i
  -- ‚ä¢ ‚Üë(F.val.map (leSupr U i).op) (‚Üë(F.val.map (eqToHom (_ : iSup U = V)).op) s)  ‚Ä¶
  rw [‚Üê comp_apply, ‚Üê comp_apply, ‚Üê F.1.map_comp]
  -- ‚ä¢ ‚Üë(F.val.map ((eqToHom (_ : iSup U = V)).op ‚â´ (leSupr U i).op)) s = ‚Üë(F.val.m ‚Ä¶
  convert h i
  -- üéâ no goals
set_option linter.uppercaseLean3 false in
#align Top.sheaf.eq_of_locally_eq' TopCat.Sheaf.eq_of_locally_eq'

theorem eq_of_locally_eq‚ÇÇ {U‚ÇÅ U‚ÇÇ V : Opens X} (i‚ÇÅ : U‚ÇÅ ‚ü∂ V) (i‚ÇÇ : U‚ÇÇ ‚ü∂ V) (hcover : V ‚â§ U‚ÇÅ ‚äî U‚ÇÇ)
    (s t : F.1.obj (op V)) (h‚ÇÅ : F.1.map i‚ÇÅ.op s = F.1.map i‚ÇÅ.op t)
    (h‚ÇÇ : F.1.map i‚ÇÇ.op s = F.1.map i‚ÇÇ.op t) : s = t := by
  classical
    fapply F.eq_of_locally_eq' fun t : ULift Bool => if t.1 then U‚ÇÅ else U‚ÇÇ
    ¬∑ exact fun i => if h : i.1 then eqToHom (if_pos h) ‚â´ i‚ÇÅ else eqToHom (if_neg h) ‚â´ i‚ÇÇ
    ¬∑ refine' le_trans hcover _
      rw [sup_le_iff]
      constructor
      ¬∑ convert le_iSup (fun t : ULift Bool => if t.1 then U‚ÇÅ else U‚ÇÇ) (ULift.up True)
      ¬∑ convert le_iSup (fun t : ULift Bool => if t.1 then U‚ÇÅ else U‚ÇÇ) (ULift.up False)
    ¬∑ rintro ‚ü®_ | _‚ü©
      any_goals exact h‚ÇÅ
      any_goals exact h‚ÇÇ
set_option linter.uppercaseLean3 false in
#align Top.sheaf.eq_of_locally_eq‚ÇÇ TopCat.Sheaf.eq_of_locally_eq‚ÇÇ

end

end Sheaf

end TopCat
