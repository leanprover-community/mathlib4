/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.CategoryTheory.Limits.Shapes.Equalizers
import Mathlib.CategoryTheory.Limits.Shapes.Products
import Mathlib.Topology.Sheaves.SheafCondition.PairwiseIntersections

#align_import topology.sheaves.sheaf_condition.equalizer_products from "leanprover-community/mathlib"@"85d6221d32c37e68f05b2e42cde6cee658dae5e9"

/-!
# The sheaf condition in terms of an equalizer of products

Here we set up the machinery for the "usual" definition of the sheaf condition,
e.g. as in https://stacks.math.columbia.edu/tag/0072
in terms of an equalizer diagram where the two objects are
`‚àè·∂ú F.obj (U i)` and `‚àè·∂ú F.obj (U i) ‚äì (U j)`.

We show that this sheaf condition is equivalent to the "pairwise intersections" sheaf condition when
the presheaf is valued in a category with products, and thereby equivalent to the default sheaf
condition.
-/


universe v' v u

noncomputable section

open CategoryTheory CategoryTheory.Limits TopologicalSpace Opposite TopologicalSpace.Opens

namespace TopCat

variable {C : Type u} [Category.{v} C] [HasProducts.{v'} C]
variable {X : TopCat.{v'}} (F : Presheaf C X) {Œπ : Type v'} (U : Œπ ‚Üí Opens X)

namespace Presheaf

namespace SheafConditionEqualizerProducts

/-- The product of the sections of a presheaf over a family of open sets. -/
def piOpens : C :=
  ‚àè·∂ú fun i : Œπ => F.obj (op (U i))
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_equalizer_products.pi_opens TopCat.Presheaf.SheafConditionEqualizerProducts.piOpens

/-- The product of the sections of a presheaf over the pairwise intersections of
a family of open sets.
-/
def piInters : C :=
  ‚àè·∂ú fun p : Œπ √ó Œπ => F.obj (op (U p.1 ‚äì U p.2))
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_equalizer_products.pi_inters TopCat.Presheaf.SheafConditionEqualizerProducts.piInters

/-- The morphism `Œ† F.obj (U i) ‚ü∂ Œ† F.obj (U i) ‚äì (U j)` whose components
are given by the restriction maps from `U i` to `U i ‚äì U j`.
-/
def leftRes : piOpens F U ‚ü∂ piInters.{v'} F U :=
  Pi.lift fun p : Œπ √ó Œπ => Pi.œÄ _ p.1 ‚â´ F.map (infLELeft (U p.1) (U p.2)).op
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_equalizer_products.left_res TopCat.Presheaf.SheafConditionEqualizerProducts.leftRes

/-- The morphism `Œ† F.obj (U i) ‚ü∂ Œ† F.obj (U i) ‚äì (U j)` whose components
are given by the restriction maps from `U j` to `U i ‚äì U j`.
-/
def rightRes : piOpens F U ‚ü∂ piInters.{v'} F U :=
  Pi.lift fun p : Œπ √ó Œπ => Pi.œÄ _ p.2 ‚â´ F.map (infLERight (U p.1) (U p.2)).op
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_equalizer_products.right_res TopCat.Presheaf.SheafConditionEqualizerProducts.rightRes

/-- The morphism `F.obj U ‚ü∂ Œ† F.obj (U i)` whose components
are given by the restriction maps from `U j` to `U i ‚äì U j`.
-/
def res : F.obj (op (iSup U)) ‚ü∂ piOpens.{v'} F U :=
  Pi.lift fun i : Œπ => F.map (TopologicalSpace.Opens.leSupr U i).op
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_equalizer_products.res TopCat.Presheaf.SheafConditionEqualizerProducts.res

@[simp, elementwise]
theorem res_œÄ (i : Œπ) : res F U ‚â´ limit.œÄ _ ‚ü®i‚ü© = F.map (Opens.leSupr U i).op := by
  rw [res, limit.lift_œÄ, Fan.mk_œÄ_app]
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_equalizer_products.res_œÄ TopCat.Presheaf.SheafConditionEqualizerProducts.res_œÄ

@[elementwise]
theorem w : res F U ‚â´ leftRes F U = res F U ‚â´ rightRes F U := by
  dsimp [res, leftRes, rightRes]
  -- Porting note: `ext` can't see `limit.hom_ext` applies here:
  -- See https://github.com/leanprover-community/mathlib4/issues/5229
  refine limit.hom_ext (fun _ => ?_)
  simp only [limit.lift_œÄ, limit.lift_œÄ_assoc, Fan.mk_œÄ_app, Category.assoc]
  rw [‚Üê F.map_comp]
  rw [‚Üê F.map_comp]
  congr 1
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_equalizer_products.w TopCat.Presheaf.SheafConditionEqualizerProducts.w

/-- The equalizer diagram for the sheaf condition.
-/
abbrev diagram : WalkingParallelPair ‚•§ C :=
  parallelPair (leftRes.{v'} F U) (rightRes F U)
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_equalizer_products.diagram TopCat.Presheaf.SheafConditionEqualizerProducts.diagram

/-- The restriction map `F.obj U ‚ü∂ Œ† F.obj (U i)` gives a cone over the equalizer diagram
for the sheaf condition. The sheaf condition asserts this cone is a limit cone.
-/
def fork : Fork.{v} (leftRes F U) (rightRes F U) :=
  Fork.ofŒπ _ (w F U)
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_equalizer_products.fork TopCat.Presheaf.SheafConditionEqualizerProducts.fork

@[simp]
theorem fork_pt : (fork F U).pt = F.obj (op (iSup U)) :=
  rfl
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_equalizer_products.fork_X TopCat.Presheaf.SheafConditionEqualizerProducts.fork_pt

@[simp]
theorem fork_Œπ : (fork F U).Œπ = res F U :=
  rfl
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_equalizer_products.fork_Œπ TopCat.Presheaf.SheafConditionEqualizerProducts.fork_Œπ

@[simp]
theorem fork_œÄ_app_walkingParallelPair_zero : (fork F U).œÄ.app WalkingParallelPair.zero = res F U :=
  rfl
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_equalizer_products.fork_œÄ_app_walking_parallel_pair_zero TopCat.Presheaf.SheafConditionEqualizerProducts.fork_œÄ_app_walkingParallelPair_zero

-- Porting note: Shortcut simplifier
@[simp (high)]
theorem fork_œÄ_app_walkingParallelPair_one :
    (fork F U).œÄ.app WalkingParallelPair.one = res F U ‚â´ leftRes F U :=
  rfl
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_equalizer_products.fork_œÄ_app_walking_parallel_pair_one TopCat.Presheaf.SheafConditionEqualizerProducts.fork_œÄ_app_walkingParallelPair_one

variable {F} {G : Presheaf C X}

/-- Isomorphic presheaves have isomorphic `piOpens` for any cover `U`. -/
@[simp]
def piOpens.isoOfIso (Œ± : F ‚âÖ G) : piOpens F U ‚âÖ piOpens.{v'} G U :=
  Pi.mapIso fun _ => Œ±.app _
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_equalizer_products.pi_opens.iso_of_iso TopCat.Presheaf.SheafConditionEqualizerProducts.piOpens.isoOfIso

/-- Isomorphic presheaves have isomorphic `piInters` for any cover `U`. -/
@[simp]
def piInters.isoOfIso (Œ± : F ‚âÖ G) : piInters F U ‚âÖ piInters.{v'} G U :=
  Pi.mapIso fun _ => Œ±.app _
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_equalizer_products.pi_inters.iso_of_iso TopCat.Presheaf.SheafConditionEqualizerProducts.piInters.isoOfIso

/-- Isomorphic presheaves have isomorphic sheaf condition diagrams. -/
def diagram.isoOfIso (Œ± : F ‚âÖ G) : diagram F U ‚âÖ diagram.{v'} G U :=
  NatIso.ofComponents (by
    rintro ‚ü®‚ü©
    ¬∑ exact piOpens.isoOfIso U Œ±
    ¬∑ exact piInters.isoOfIso U Œ±)
    (by
      rintro ‚ü®‚ü© ‚ü®‚ü© ‚ü®‚ü©
      ¬∑ simp
      ¬∑ -- Porting note: `ext` can't see `limit.hom_ext` applies here:
        -- See https://github.com/leanprover-community/mathlib4/issues/5229
        refine limit.hom_ext (fun _ => ?_)
        simp only [leftRes, piOpens.isoOfIso, piInters.isoOfIso, parallelPair_map_left,
          Functor.mapIso_hom, lim_map, limit.lift_map, limit.lift_œÄ, Cones.postcompose_obj_œÄ,
          NatTrans.comp_app, Fan.mk_œÄ_app, Discrete.natIso_hom_app, Iso.app_hom, Category.assoc,
          NatTrans.naturality, limMap_œÄ_assoc]
      ¬∑ -- Porting note: `ext` can't see `limit.hom_ext` applies here:
        -- See https://github.com/leanprover-community/mathlib4/issues/5229
        refine limit.hom_ext (fun _ => ?_)
        simp only [rightRes, piOpens.isoOfIso, piInters.isoOfIso, parallelPair_map_right,
          Functor.mapIso_hom, lim_map, limit.lift_map, limit.lift_œÄ, Cones.postcompose_obj_œÄ,
          NatTrans.comp_app, Fan.mk_œÄ_app, Discrete.natIso_hom_app, Iso.app_hom, Category.assoc,
          NatTrans.naturality, limMap_œÄ_assoc]
      ¬∑ simp)
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_equalizer_products.diagram.iso_of_iso TopCat.Presheaf.SheafConditionEqualizerProducts.diagram.isoOfIso

/-- If `F G : presheaf C X` are isomorphic presheaves,
then the `fork F U`, the canonical cone of the sheaf condition diagram for `F`,
is isomorphic to `fork F G` postcomposed with the corresponding isomorphism between
sheaf condition diagrams.
-/
def fork.isoOfIso (Œ± : F ‚âÖ G) :
    fork F U ‚âÖ (Cones.postcompose (diagram.isoOfIso U Œ±).inv).obj (fork G U) := by
  fapply Fork.ext
  ¬∑ apply Œ±.app
  ¬∑ -- Porting note: `ext` can't see `limit.hom_ext` applies here:
    -- See https://github.com/leanprover-community/mathlib4/issues/5229
    refine limit.hom_ext (fun _ => ?_)
    dsimp only [Fork.Œπ]
    -- Ugh, `simp` can't unfold abbreviations.
    simp only [res, diagram.isoOfIso, Iso.app_hom, piOpens.isoOfIso, Cones.postcompose_obj_œÄ,
      NatTrans.comp_app, fork_œÄ_app_walkingParallelPair_zero, NatIso.ofComponents_inv_app,
      Functor.mapIso_inv, lim_map, limit.lift_map, Category.assoc, limit.lift_œÄ, Fan.mk_œÄ_app,
      Discrete.natIso_inv_app, Iso.app_inv, NatTrans.naturality, Iso.hom_inv_id_app_assoc]
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_equalizer_products.fork.iso_of_iso TopCat.Presheaf.SheafConditionEqualizerProducts.fork.isoOfIso

end SheafConditionEqualizerProducts

/-- The sheaf condition for a `F : presheaf C X` requires that the morphism
`F.obj U ‚ü∂ ‚àè·∂ú F.obj (U i)` (where `U` is some open set which is the union of the `U i`)
is the equalizer of the two morphisms
`‚àè·∂ú F.obj (U i) ‚ü∂ ‚àè·∂ú F.obj (U i) ‚äì (U j)`.
-/
def IsSheafEqualizerProducts (F : Presheaf.{v', v, u} C X) : Prop :=
  ‚àÄ ‚¶ÉŒπ : Type v'‚¶Ñ (U : Œπ ‚Üí Opens X), Nonempty (IsLimit (SheafConditionEqualizerProducts.fork F U))
set_option linter.uppercaseLean3 false in
#align Top.presheaf.is_sheaf_equalizer_products TopCat.Presheaf.IsSheafEqualizerProducts

/-!
The remainder of this file shows that the "equalizer products" sheaf condition is equivalent
to the "pairwise intersections" sheaf condition.
-/


namespace SheafConditionPairwiseIntersections

open CategoryTheory.Pairwise CategoryTheory.Pairwise.Hom

/-- Implementation of `SheafConditionPairwiseIntersections.coneEquiv`. -/
@[simps]
def coneEquivFunctorObj (c : Cone ((diagram U).op ‚ãô F)) :
    Cone (SheafConditionEqualizerProducts.diagram F U) where
  pt := c.pt
  œÄ :=
    { app := fun Z =>
        WalkingParallelPair.casesOn Z (Pi.lift fun i : Œπ => c.œÄ.app (op (single i)))
          (Pi.lift fun b : Œπ √ó Œπ => c.œÄ.app (op (pair b.1 b.2)))
      naturality := fun Y Z f => by
        cases Y <;> cases Z <;> cases f
        ¬∑ -- Porting note: `ext` can't see `limit.hom_ext` applies here:
          -- See https://github.com/leanprover-community/mathlib4/issues/5229
          refine limit.hom_ext fun i => ?_
          dsimp
          simp only [limit.lift_œÄ, Category.id_comp, Fan.mk_œÄ_app, CategoryTheory.Functor.map_id,
            Category.assoc]
          dsimp
          simp only [limit.lift_œÄ, Category.id_comp, Fan.mk_œÄ_app]
        ¬∑ -- Porting note: `ext` can't see `limit.hom_ext` applies here:
          -- See https://github.com/leanprover-community/mathlib4/issues/5229
          refine limit.hom_ext fun ‚ü®i, j‚ü© => ?_
          dsimp [SheafConditionEqualizerProducts.leftRes]
          simp only [limit.lift_œÄ, limit.lift_œÄ_assoc, Category.id_comp, Fan.mk_œÄ_app,
            Category.assoc]
          have h := c.œÄ.naturality (Quiver.Hom.op (Hom.left i j))
          dsimp at h
          simpa using h
        ¬∑ -- Porting note: `ext` can't see `limit.hom_ext` applies here:
          -- See https://github.com/leanprover-community/mathlib4/issues/5229
          refine limit.hom_ext fun ‚ü®i, j‚ü© => ?_
          dsimp [SheafConditionEqualizerProducts.rightRes]
          simp only [limit.lift_œÄ, limit.lift_œÄ_assoc, Category.id_comp, Fan.mk_œÄ_app,
            Category.assoc]
          have h := c.œÄ.naturality (Quiver.Hom.op (Hom.right i j))
          dsimp at h
          simpa using h
        ¬∑ -- Porting note: `ext` can't see `limit.hom_ext` applies here:
          -- See https://github.com/leanprover-community/mathlib4/issues/5229
          refine limit.hom_ext fun i => ?_
          dsimp
          simp only [limit.lift_œÄ, Category.id_comp, Fan.mk_œÄ_app, CategoryTheory.Functor.map_id,
            Category.assoc]
          dsimp
          simp only [limit.lift_œÄ, Category.id_comp, Fan.mk_œÄ_app] }
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_pairwise_intersections.cone_equiv_functor_obj TopCat.Presheaf.SheafConditionPairwiseIntersections.coneEquivFunctorObj

section

/-- Implementation of `SheafConditionPairwiseIntersections.coneEquiv`. -/
@[simps!]
def coneEquivFunctor :
    Limits.Cone ((diagram U).op ‚ãô F) ‚•§
      Limits.Cone (SheafConditionEqualizerProducts.diagram F U) where
  obj c := coneEquivFunctorObj F U c
  map {c c'} f :=
    { hom := f.hom
      w := fun j => by
        cases j <;>
          ¬∑ -- Porting note: `ext` can't see `limit.hom_ext` applies here:
            -- See https://github.com/leanprover-community/mathlib4/issues/5229
            refine limit.hom_ext fun i => ?_
            simp only [Limits.Fan.mk_œÄ_app, Limits.ConeMorphism.w, Limits.limit.lift_œÄ,
              Category.assoc, coneEquivFunctorObj_œÄ_app] }
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_pairwise_intersections.cone_equiv_functor TopCat.Presheaf.SheafConditionPairwiseIntersections.coneEquivFunctor

end

/-- Implementation of `SheafConditionPairwiseIntersections.coneEquiv`. -/
@[simps]
def coneEquivInverseObj (c : Limits.Cone (SheafConditionEqualizerProducts.diagram F U)) :
    Limits.Cone ((diagram U).op ‚ãô F) where
  pt := c.pt
  œÄ :=
    { app := by
        intro x
        induction x using Opposite.rec' with | h x => ?_
        rcases x with (‚ü®i‚ü© | ‚ü®i, j‚ü©)
        ¬∑ exact c.œÄ.app WalkingParallelPair.zero ‚â´ Pi.œÄ _ i
        ¬∑ exact c.œÄ.app WalkingParallelPair.one ‚â´ Pi.œÄ _ (i, j)
      naturality := by
        intro x y f
        induction x using Opposite.rec' with | h x => ?_
        induction y using Opposite.rec' with | h y => ?_
        have ef : f = f.unop.op := rfl
        revert ef
        generalize f.unop = f'
        rintro rfl
        rcases x with (‚ü®i‚ü© | ‚ü®‚ü©) <;> rcases y with (‚ü®‚ü© | ‚ü®j, j‚ü©) <;> rcases f' with ‚ü®‚ü©
        ¬∑ dsimp
          erw [F.map_id]
          simp
        ¬∑ dsimp
          simp only [Category.id_comp, Category.assoc]
          have h := c.œÄ.naturality WalkingParallelPairHom.left
          dsimp [SheafConditionEqualizerProducts.leftRes] at h
          simp only [Category.id_comp] at h
          have h' := h =‚â´ Pi.œÄ _ (i, j)
          rw [h']
          simp only [Category.assoc, limit.lift_œÄ, Fan.mk_œÄ_app]
          rfl
        ¬∑ dsimp
          simp only [Category.id_comp, Category.assoc]
          have h := c.œÄ.naturality WalkingParallelPairHom.right
          dsimp [SheafConditionEqualizerProducts.rightRes] at h
          simp only [Category.id_comp] at h
          have h' := h =‚â´ Pi.œÄ _ (j, i)
          rw [h']
          simp
          rfl
        ¬∑ dsimp
          erw [F.map_id]
          simp }
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_pairwise_intersections.cone_equiv_inverse_obj TopCat.Presheaf.SheafConditionPairwiseIntersections.coneEquivInverseObj

/-- Implementation of `SheafConditionPairwiseIntersections.coneEquiv`. -/
@[simps!]
def coneEquivInverse :
    Limits.Cone (SheafConditionEqualizerProducts.diagram F U) ‚•§
      Limits.Cone ((diagram U).op ‚ãô F) where
  obj c := coneEquivInverseObj F U c
  map {c c'} f :=
    { hom := f.hom
      w := by
        intro x
        induction x using Opposite.rec' with | h x => ?_
        rcases x with (‚ü®i‚ü© | ‚ü®i, j‚ü©)
        ¬∑ dsimp
          dsimp only [Fork.Œπ]
          rw [‚Üê f.w WalkingParallelPair.zero, Category.assoc]
        ¬∑ dsimp
          rw [‚Üê f.w WalkingParallelPair.one, Category.assoc] }
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_pairwise_intersections.cone_equiv_inverse TopCat.Presheaf.SheafConditionPairwiseIntersections.coneEquivInverse

/-- Implementation of `SheafConditionPairwiseIntersections.coneEquiv`. -/
@[simps]
def coneEquivUnitIsoApp (c : Cone ((diagram U).op ‚ãô F)) :
    (ùü≠ (Cone ((diagram U).op ‚ãô F))).obj c ‚âÖ
      (coneEquivFunctor F U ‚ãô coneEquivInverse F U).obj c where
  hom :=
    { hom := ùüô _
      w := fun j => by
        induction j using Opposite.rec' with | h j => ?_;
        rcases j with ‚ü®‚ü© <;>
        ¬∑ dsimp [coneEquivInverse]
          simp only [Limits.Fan.mk_œÄ_app, Category.id_comp, Limits.limit.lift_œÄ] }
  inv :=
    { hom := ùüô _
      w := fun j => by
        induction j using Opposite.rec' with | h j => ?_;
        rcases j with ‚ü®‚ü© <;>
        ¬∑ dsimp [coneEquivInverse]
          simp only [Limits.Fan.mk_œÄ_app, Category.id_comp, Limits.limit.lift_œÄ] }
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_pairwise_intersections.cone_equiv_unit_iso_app TopCat.Presheaf.SheafConditionPairwiseIntersections.coneEquivUnitIsoApp

/-- Implementation of `SheafConditionPairwiseIntersections.coneEquiv`. -/
@[simps!]
def coneEquivUnitIso :
    ùü≠ (Limits.Cone ((diagram U).op ‚ãô F)) ‚âÖ coneEquivFunctor F U ‚ãô coneEquivInverse F U :=
  NatIso.ofComponents (coneEquivUnitIsoApp F U)
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_pairwise_intersections.cone_equiv_unit_iso TopCat.Presheaf.SheafConditionPairwiseIntersections.coneEquivUnitIso

/-- Implementation of `SheafConditionPairwiseIntersections.coneEquiv`. -/
@[simps!]
def coneEquivCounitIso :
    coneEquivInverse F U ‚ãô coneEquivFunctor F U ‚âÖ
      ùü≠ (Limits.Cone (SheafConditionEqualizerProducts.diagram F U)) :=
  NatIso.ofComponents
    (fun c =>
      { hom :=
          { hom := ùüô _
            w := by
              rintro ‚ü®_ | _‚ü©
              ¬∑ -- Porting note: `ext` can't see `limit.hom_ext` applies here:
                -- See https://github.com/leanprover-community/mathlib4/issues/5229
                refine limit.hom_ext fun ‚ü®j‚ü© => ?_
                dsimp [coneEquivInverse]
                simp only [Limits.Fan.mk_œÄ_app, Category.id_comp, Limits.limit.lift_œÄ]
              ¬∑ -- Porting note: `ext` can't see `limit.hom_ext` applies here:
                -- See https://github.com/leanprover-community/mathlib4/issues/5229
                refine limit.hom_ext fun ‚ü®i, j‚ü© => ?_
                dsimp [coneEquivInverse]
                simp only [Limits.Fan.mk_œÄ_app, Category.id_comp, Limits.limit.lift_œÄ] }
        inv :=
          { hom := ùüô _
            w := by
              rintro ‚ü®_ | _‚ü©
              ¬∑ -- Porting note: `ext` can't see `limit.hom_ext` applies here:
                -- See https://github.com/leanprover-community/mathlib4/issues/5229
                refine limit.hom_ext fun ‚ü®j‚ü© => ?_
                dsimp [coneEquivInverse]
                simp only [Limits.Fan.mk_œÄ_app, Category.id_comp, Limits.limit.lift_œÄ]
              ¬∑ -- Porting note: `ext` can't see `limit.hom_ext` applies here:
                -- See https://github.com/leanprover-community/mathlib4/issues/5229
                refine limit.hom_ext fun ‚ü®i, j‚ü© => ?_
                dsimp [coneEquivInverse]
                simp only [Limits.Fan.mk_œÄ_app, Category.id_comp, Limits.limit.lift_œÄ] } })
    fun {c d} f => by
    ext
    dsimp
    simp only [Category.comp_id, Category.id_comp]
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_pairwise_intersections.cone_equiv_counit_iso TopCat.Presheaf.SheafConditionPairwiseIntersections.coneEquivCounitIso

/--
Cones over `diagram U ‚ãô F` are the same as a cones over the usual sheaf condition equalizer diagram.
-/
@[simps]
def coneEquiv :
    Limits.Cone ((diagram U).op ‚ãô F) ‚âå
      Limits.Cone (SheafConditionEqualizerProducts.diagram F U) where
  functor := coneEquivFunctor F U
  inverse := coneEquivInverse F U
  unitIso := coneEquivUnitIso F U
  counitIso := coneEquivCounitIso F U
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_pairwise_intersections.cone_equiv TopCat.Presheaf.SheafConditionPairwiseIntersections.coneEquiv

-- Porting note: not supported in Lean 4
-- attribute [local reducible]
--   SheafConditionEqualizerProducts.res SheafConditionEqualizerProducts.leftRes

/-- If `SheafConditionEqualizerProducts.fork` is an equalizer,
then `F.mapCone (cone U)` is a limit cone.
-/
def isLimitMapConeOfIsLimitSheafConditionFork
    (P : IsLimit (SheafConditionEqualizerProducts.fork F U)) : IsLimit (F.mapCone (cocone U).op) :=
  IsLimit.ofIsoLimit ((IsLimit.ofConeEquiv (coneEquiv F U).symm).symm P)
    { hom :=
        { hom := ùüô _
          w := by
            intro x
            induction x with | h x => ?_
            rcases x with ‚ü®‚ü©
            ¬∑ simp
              rfl
            ¬∑ dsimp [coneEquivInverse, SheafConditionEqualizerProducts.res,
                SheafConditionEqualizerProducts.leftRes]
              simp only [limit.lift_œÄ, limit.lift_œÄ_assoc, Category.id_comp, Fan.mk_œÄ_app,
                Category.assoc]
              rw [‚Üê F.map_comp]
              rfl }
      inv :=
        { hom := ùüô _
          w := by
            intro x
            induction x with | h x => ?_
            rcases x with ‚ü®‚ü©
            ¬∑ simp
              rfl
            ¬∑ dsimp [coneEquivInverse, SheafConditionEqualizerProducts.res,
                SheafConditionEqualizerProducts.leftRes]
              simp only [limit.lift_œÄ, limit.lift_œÄ_assoc, Category.id_comp, Fan.mk_œÄ_app,
                Category.assoc]
              rw [‚Üê F.map_comp]
              rfl } }
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_pairwise_intersections.is_limit_map_cone_of_is_limit_sheaf_condition_fork TopCat.Presheaf.SheafConditionPairwiseIntersections.isLimitMapConeOfIsLimitSheafConditionFork

/-- If `F.mapCone (cone U)` is a limit cone,
then `SheafConditionEqualizerProducts.fork` is an equalizer.
-/
def isLimitSheafConditionForkOfIsLimitMapCone (Q : IsLimit (F.mapCone (cocone U).op)) :
    IsLimit (SheafConditionEqualizerProducts.fork F U) :=
  IsLimit.ofIsoLimit ((IsLimit.ofConeEquiv (coneEquiv F U)).symm Q)
    { hom :=
        { hom := ùüô _
          w := by
            rintro ‚ü®‚ü©
            ¬∑ simp
              rfl
            ¬∑ -- Porting note: `ext` can't see `limit.hom_ext` applies here:
              -- See https://github.com/leanprover-community/mathlib4/issues/5229
              refine limit.hom_ext fun ‚ü®i, j‚ü© => ?_
              dsimp [coneEquivInverse, SheafConditionEqualizerProducts.res,
                SheafConditionEqualizerProducts.leftRes]
              simp only [limit.lift_œÄ, limit.lift_œÄ_assoc, Category.id_comp, Fan.mk_œÄ_app,
                Category.assoc]
              rw [‚Üê F.map_comp]
              rfl }
      inv :=
        { hom := ùüô _
          w := by
            rintro ‚ü®‚ü©
            ¬∑ simp
              rfl
            ¬∑ -- Porting note: `ext` can't see `limit.hom_ext` applies here:
              -- See https://github.com/leanprover-community/mathlib4/issues/5229
              refine limit.hom_ext fun ‚ü®i, j‚ü© => ?_
              dsimp [coneEquivInverse, SheafConditionEqualizerProducts.res,
                SheafConditionEqualizerProducts.leftRes]
              simp only [limit.lift_œÄ, limit.lift_œÄ_assoc, Category.id_comp, Fan.mk_œÄ_app,
                Category.assoc]
              rw [‚Üê F.map_comp]
              rfl } }
set_option linter.uppercaseLean3 false in
#align Top.presheaf.sheaf_condition_pairwise_intersections.is_limit_sheaf_condition_fork_of_is_limit_map_cone TopCat.Presheaf.SheafConditionPairwiseIntersections.isLimitSheafConditionForkOfIsLimitMapCone

end SheafConditionPairwiseIntersections

open SheafConditionPairwiseIntersections

/-- The sheaf condition in terms of an equalizer diagram is equivalent
to the default sheaf condition.
-/
theorem isSheaf_iff_isSheafEqualizerProducts (F : Presheaf C X) :
    F.IsSheaf ‚Üî F.IsSheafEqualizerProducts :=
  (isSheaf_iff_isSheafPairwiseIntersections F).trans <|
    Iff.intro (fun h _ U => ‚ü®isLimitSheafConditionForkOfIsLimitMapCone F U (h U).some‚ü©) fun h _ U =>
      ‚ü®isLimitMapConeOfIsLimitSheafConditionFork F U (h U).some‚ü©
set_option linter.uppercaseLean3 false in
#align Top.presheaf.is_sheaf_iff_is_sheaf_equalizer_products TopCat.Presheaf.isSheaf_iff_isSheafEqualizerProducts

end Presheaf

end TopCat
