/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Scott Morrison, Adam Topaz
-/
import Mathlib.Topology.Sheaves.SheafOfFunctions
import Mathlib.Topology.Sheaves.Stalks
import Mathlib.Topology.Sheaves.SheafCondition.UniqueGluing

#align_import topology.sheaves.local_predicate from "leanprover-community/mathlib"@"5dc6092d09e5e489106865241986f7f2ad28d4c8"

/-!
# Functions satisfying a local predicate form a sheaf.

At this stage, in `Mathlib/Topology/Sheaves/SheafOfFunctions.lean`
we've proved that not-necessarily-continuous functions from a topological space
into some type (or type family) form a sheaf.

Why do the continuous functions form a sheaf?
The point is just that continuity is a local condition,
so one can use the lifting condition for functions to provide a candidate lift,
then verify that the lift is actually continuous by using the factorisation condition for the lift
(which guarantees that on each open set it agrees with the functions being lifted,
which were assumed to be continuous).

This file abstracts this argument to work for
any collection of dependent functions on a topological space
satisfying a "local predicate".

As an application, we check that continuity is a local predicate in this sense, and provide
* `TopCat.sheafToTop`: continuous functions into a topological space form a sheaf

A sheaf constructed in this way has a natural map `stalkToFiber` from the stalks
to the types in the ambient type family.

We give conditions sufficient to show that this map is injective and/or surjective.
-/


universe v

noncomputable section

variable {X : TopCat.{v}}
variable (T : X ‚Üí Type v)

open TopologicalSpace

open Opposite

open CategoryTheory

open CategoryTheory.Limits

open CategoryTheory.Limits.Types

namespace TopCat

/-- Given a topological space `X : TopCat` and a type family `T : X ‚Üí Type`,
a `P : PrelocalPredicate T` consists of:
* a family of predicates `P.pred`, one for each `U : Opens X`, of the form `(Œ† x : U, T x) ‚Üí Prop`
* a proof that if `f : Œ† x : V, T x` satisfies the predicate on `V : Opens X`, then
  the restriction of `f` to any open subset `U` also satisfies the predicate.
-/
structure PrelocalPredicate where
  /-- The underlying predicate of a prelocal predicate -/
  pred : ‚àÄ {U : Opens X}, (‚àÄ x : U, T x) ‚Üí Prop
  /-- The underlying predicate should be invariant under restriction -/
  res : ‚àÄ {U V : Opens X} (i : U ‚ü∂ V) (f : ‚àÄ x : V, T x) (_ : pred f), pred fun x : U => f (i x)
set_option linter.uppercaseLean3 false in
#align Top.prelocal_predicate TopCat.PrelocalPredicate

variable (X)

/-- Continuity is a "prelocal" predicate on functions to a fixed topological space `T`.
-/
@[simps!]
def continuousPrelocal (T : TopCat.{v}) : PrelocalPredicate fun _ : X => T where
  pred {_} f := Continuous f
  res {_ _} i _ h := Continuous.comp h (Opens.openEmbedding_of_le i.le).continuous
set_option linter.uppercaseLean3 false in
#align Top.continuous_prelocal TopCat.continuousPrelocal

/-- Satisfying the inhabited linter. -/
instance inhabitedPrelocalPredicate (T : TopCat.{v}) :
    Inhabited (PrelocalPredicate fun _ : X => T) :=
  ‚ü®continuousPrelocal X T‚ü©
set_option linter.uppercaseLean3 false in
#align Top.inhabited_prelocal_predicate TopCat.inhabitedPrelocalPredicate

variable {X}

/-- Given a topological space `X : TopCat` and a type family `T : X ‚Üí Type`,
a `P : LocalPredicate T` consists of:
* a family of predicates `P.pred`, one for each `U : Opens X`, of the form `(Œ† x : U, T x) ‚Üí Prop`
* a proof that if `f : Œ† x : V, T x` satisfies the predicate on `V : Opens X`, then
  the restriction of `f` to any open subset `U` also satisfies the predicate, and
* a proof that given some `f : Œ† x : U, T x`,
  if for every `x : U` we can find an open set `x ‚àà V ‚â§ U`
  so that the restriction of `f` to `V` satisfies the predicate,
  then `f` itself satisfies the predicate.
-/
structure LocalPredicate extends PrelocalPredicate T where
  /-- A local predicate must be local --- provided that it is locally satisfied, it is also globally
    satisfied -/
  locality :
    ‚àÄ {U : Opens X} (f : ‚àÄ x : U, T x)
      (_ : ‚àÄ x : U, ‚àÉ (V : Opens X) (_ : x.1 ‚àà V) (i : V ‚ü∂ U),
        pred fun x : V => f (i x : U)), pred f
set_option linter.uppercaseLean3 false in
#align Top.local_predicate TopCat.LocalPredicate

variable (X)

/-- Continuity is a "local" predicate on functions to a fixed topological space `T`.
-/
def continuousLocal (T : TopCat.{v}) : LocalPredicate fun _ : X => T :=
  { continuousPrelocal X T with
    locality := fun {U} f w => by
      apply continuous_iff_continuousAt.2
      intro x
      specialize w x
      rcases w with ‚ü®V, m, i, w‚ü©
      dsimp at w
      rw [continuous_iff_continuousAt] at w
      specialize w ‚ü®x, m‚ü©
      simpa using (Opens.openEmbedding_of_le i.le).continuousAt_iff.1 w }
set_option linter.uppercaseLean3 false in
#align Top.continuous_local TopCat.continuousLocal

/-- Satisfying the inhabited linter. -/
instance inhabitedLocalPredicate (T : TopCat.{v}) : Inhabited (LocalPredicate fun _ : X => T) :=
  ‚ü®continuousLocal X T‚ü©
set_option linter.uppercaseLean3 false in
#align Top.inhabited_local_predicate TopCat.inhabitedLocalPredicate

variable {X T}

/-- Given a `P : PrelocalPredicate`, we can always construct a `LocalPredicate`
by asking that the condition from `P` holds locally near every point.
-/
def PrelocalPredicate.sheafify {T : X ‚Üí Type v} (P : PrelocalPredicate T) : LocalPredicate T where
  pred {U} f := ‚àÄ x : U, ‚àÉ (V : Opens X) (_ : x.1 ‚àà V) (i : V ‚ü∂ U), P.pred fun x : V => f (i x : U)
  res {V U} i f w x := by
    specialize w (i x)
    rcases w with ‚ü®V', m', i', p‚ü©
    refine ‚ü®V ‚äì V', ‚ü®x.2, m'‚ü©, Opens.infLELeft _ _, ?_‚ü©
    convert P.res (Opens.infLERight V V') _ p
  locality {U} f w x := by
    specialize w x
    rcases w with ‚ü®V, m, i, p‚ü©
    specialize p ‚ü®x.1, m‚ü©
    rcases p with ‚ü®V', m', i', p'‚ü©
    exact ‚ü®V', m', i' ‚â´ i, p'‚ü©
set_option linter.uppercaseLean3 false in
#align Top.prelocal_predicate.sheafify TopCat.PrelocalPredicate.sheafify

theorem PrelocalPredicate.sheafifyOf {T : X ‚Üí Type v} {P : PrelocalPredicate T} {U : Opens X}
    {f : ‚àÄ x : U, T x} (h : P.pred f) : P.sheafify.pred f := fun x =>
  ‚ü®U, x.2, ùüô _, by convert h‚ü©
set_option linter.uppercaseLean3 false in
#align Top.prelocal_predicate.sheafify_of TopCat.PrelocalPredicate.sheafifyOf

/-- The subpresheaf of dependent functions on `X` satisfying the "pre-local" predicate `P`.
-/
@[simps!]
def subpresheafToTypes (P : PrelocalPredicate T) : Presheaf (Type v) X where
  obj U := { f : ‚àÄ x : U.unop , T x // P.pred f }
  map {U V} i f := ‚ü®fun x => f.1 (i.unop x), P.res i.unop f.1 f.2‚ü©
set_option linter.uppercaseLean3 false in
#align Top.subpresheaf_to_Types TopCat.subpresheafToTypes

namespace subpresheafToTypes

variable (P : PrelocalPredicate T)

/-- The natural transformation including the subpresheaf of functions satisfying a local predicate
into the presheaf of all functions.
-/
def subtype : subpresheafToTypes P ‚ü∂ presheafToTypes X T where app U f := f.1
set_option linter.uppercaseLean3 false in
#align Top.subpresheaf_to_Types.subtype TopCat.subpresheafToTypes.subtype

open TopCat.Presheaf

/-- The functions satisfying a local predicate satisfy the sheaf condition.
-/
theorem isSheaf (P : LocalPredicate T) : (subpresheafToTypes P.toPrelocalPredicate).IsSheaf :=
  Presheaf.isSheaf_of_isSheafUniqueGluing_types.{v} _ fun Œπ U sf sf_comp => by
    -- We show the sheaf condition in terms of unique gluing.
    -- First we obtain a family of sections for the underlying sheaf of functions,
    -- by forgetting that the predicate holds
    let sf' : ‚àÄ i : Œπ, (presheafToTypes X T).obj (op (U i)) := fun i => (sf i).val
    -- Since our original family is compatible, this one is as well
    have sf'_comp : (presheafToTypes X T).IsCompatible U sf' := fun i j =>
      congr_arg Subtype.val (sf_comp i j)
    -- So, we can obtain a unique gluing
    obtain ‚ü®gl, gl_spec, gl_uniq‚ü© := (sheafToTypes X T).existsUnique_gluing U sf' sf'_comp
    refine ‚ü®‚ü®gl, ?_‚ü©, ?_, ?_‚ü©
    ¬∑ -- Our first goal is to show that this chosen gluing satisfies the
      -- predicate. Of course, we use locality of the predicate.
      apply P.locality
      rintro ‚ü®x, mem‚ü©
      -- Once we're at a particular point `x`, we can select some open set `x ‚àà U i`.
      choose i hi using Opens.mem_iSup.mp mem
      -- We claim that the predicate holds in `U i`
      use U i, hi, Opens.leSupr U i
      -- This follows, since our original family `sf` satisfies the predicate
      convert (sf i).property using 1
      exact gl_spec i

    -- It remains to show that the chosen lift is really a gluing for the subsheaf and
    -- that it is unique. Both of which follow immediately from the corresponding facts
    -- in the sheaf of functions without the local predicate.
    ¬∑ exact fun i => Subtype.ext (gl_spec i)
    ¬∑ intro gl' hgl'
      refine Subtype.ext ?_
      exact gl_uniq gl'.1 fun i => congr_arg Subtype.val (hgl' i)
set_option linter.uppercaseLean3 false in
#align Top.subpresheaf_to_Types.is_sheaf TopCat.subpresheafToTypes.isSheaf

end subpresheafToTypes

/-- The subsheaf of the sheaf of all dependently typed functions satisfying the local predicate `P`.
-/
@[simps]
def subsheafToTypes (P : LocalPredicate T) : Sheaf (Type v) X :=
  ‚ü®subpresheafToTypes P.toPrelocalPredicate, subpresheafToTypes.isSheaf P‚ü©
set_option linter.uppercaseLean3 false in
#align Top.subsheaf_to_Types TopCat.subsheafToTypes

/-- There is a canonical map from the stalk to the original fiber, given by evaluating sections.
-/
def stalkToFiber (P : LocalPredicate T) (x : X) : (subsheafToTypes P).presheaf.stalk x ‚ü∂ T x := by
  refine
    colimit.desc _
      { pt := T x
        Œπ :=
          { app := fun U f => ?_
            naturality := ?_ } }
  ¬∑ exact f.1 ‚ü®x, (unop U).2‚ü©
  ¬∑ aesop
set_option linter.uppercaseLean3 false in
#align Top.stalk_to_fiber TopCat.stalkToFiber

-- Porting note (#11119): removed `simp` attribute,
-- due to left hand side is not in simple normal form.
theorem stalkToFiber_germ (P : LocalPredicate T) (U : Opens X) (x : U) (f) :
    stalkToFiber P x ((subsheafToTypes P).presheaf.germ x f) = f.1 x := by
  dsimp [Presheaf.germ, stalkToFiber]
  cases x
  simp
set_option linter.uppercaseLean3 false in
#align Top.stalk_to_fiber_germ TopCat.stalkToFiber_germ

/-- The `stalkToFiber` map is surjective at `x` if
every point in the fiber `T x` has an allowed section passing through it.
-/
theorem stalkToFiber_surjective (P : LocalPredicate T) (x : X)
    (w : ‚àÄ t : T x, ‚àÉ (U : OpenNhds x) (f : ‚àÄ y : U.1, T y) (_ : P.pred f), f ‚ü®x, U.2‚ü© = t) :
    Function.Surjective (stalkToFiber P x) := fun t => by
  rcases w t with ‚ü®U, f, h, rfl‚ü©
  fconstructor
  ¬∑ exact (subsheafToTypes P).presheaf.germ ‚ü®x, U.2‚ü© ‚ü®f, h‚ü©
  ¬∑ exact stalkToFiber_germ _ U.1 ‚ü®x, U.2‚ü© ‚ü®f, h‚ü©
set_option linter.uppercaseLean3 false in
#align Top.stalk_to_fiber_surjective TopCat.stalkToFiber_surjective

/-- The `stalkToFiber` map is injective at `x` if any two allowed sections which agree at `x`
agree on some neighborhood of `x`.
-/
theorem stalkToFiber_injective (P : LocalPredicate T) (x : X)
    (w :
      ‚àÄ (U V : OpenNhds x) (fU : ‚àÄ y : U.1, T y) (_ : P.pred fU) (fV : ‚àÄ y : V.1, T y)
        (_ : P.pred fV) (_ : fU ‚ü®x, U.2‚ü© = fV ‚ü®x, V.2‚ü©),
        ‚àÉ (W : OpenNhds x) (iU : W ‚ü∂ U) (iV : W ‚ü∂ V), ‚àÄ w : W.1,
          fU (iU w : U.1) = fV (iV w : V.1)) :
    Function.Injective (stalkToFiber P x) := fun tU tV h => by
  -- We promise to provide all the ingredients of the proof later:
  let Q :
    ‚àÉ (W : (OpenNhds x)·µí·µñ) (s : ‚àÄ w : (unop W).1, T w) (hW : P.pred s),
      tU = (subsheafToTypes P).presheaf.germ ‚ü®x, (unop W).2‚ü© ‚ü®s, hW‚ü© ‚àß
        tV = (subsheafToTypes P).presheaf.germ ‚ü®x, (unop W).2‚ü© ‚ü®s, hW‚ü© :=
    ?_
  ¬∑ choose W s hW e using Q
    exact e.1.trans e.2.symm
  -- Then use induction to pick particular representatives of `tU tV : stalk x`
  obtain ‚ü®U, ‚ü®fU, hU‚ü©, rfl‚ü© := jointly_surjective'.{v, v} tU
  obtain ‚ü®V, ‚ü®fV, hV‚ü©, rfl‚ü© := jointly_surjective'.{v, v} tV
  -- Decompose everything into its constituent parts:
  dsimp
  simp only [stalkToFiber, Types.Colimit.Œπ_desc_apply'] at h
  specialize w (unop U) (unop V) fU hU fV hV h
  rcases w with ‚ü®W, iU, iV, w‚ü©
  -- and put it back together again in the correct order.
  refine ‚ü®op W, fun w => fU (iU w : (unop U).1), P.res ?_ _ hU, ?_‚ü©
  ¬∑ rcases W with ‚ü®W, m‚ü©
    exact iU
  ¬∑ exact ‚ü®colimit_sound iU.op (Subtype.eq rfl), colimit_sound iV.op (Subtype.eq (funext w).symm)‚ü©
set_option linter.uppercaseLean3 false in
#align Top.stalk_to_fiber_injective TopCat.stalkToFiber_injective

/-- Some repackaging:
the presheaf of functions satisfying `continuousPrelocal` is just the same thing as
the presheaf of continuous functions.
-/
def subpresheafContinuousPrelocalIsoPresheafToTop (T : TopCat.{v}) :
    subpresheafToTypes (continuousPrelocal X T) ‚âÖ presheafToTop X T :=
  NatIso.ofComponents fun X =>
    { hom := by rintro ‚ü®f, c‚ü©; exact ‚ü®f, c‚ü©
      inv := by rintro ‚ü®f, c‚ü©; exact ‚ü®f, c‚ü© }
set_option linter.uppercaseLean3 false in
#align Top.subpresheaf_continuous_prelocal_iso_presheaf_to_Top TopCat.subpresheafContinuousPrelocalIsoPresheafToTop

/-- The sheaf of continuous functions on `X` with values in a space `T`.
-/
def sheafToTop (T : TopCat.{v}) : Sheaf (Type v) X :=
  ‚ü®presheafToTop X T,
    Presheaf.isSheaf_of_iso (subpresheafContinuousPrelocalIsoPresheafToTop T)
      (subpresheafToTypes.isSheaf (continuousLocal X T))‚ü©
set_option linter.uppercaseLean3 false in
#align Top.sheaf_to_Top TopCat.sheafToTop

end TopCat
