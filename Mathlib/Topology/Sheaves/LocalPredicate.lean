/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Kim Morrison, Adam Topaz
-/
module

public import Mathlib.Topology.Sheaves.SheafOfFunctions
public import Mathlib.Topology.Sheaves.Stalks
public import Mathlib.Topology.Sheaves.SheafCondition.UniqueGluing

/-!
# Functions satisfying a local predicate form a sheaf.

At this stage, in `Mathlib/Topology/Sheaves/SheafOfFunctions.lean`
we've proved that not-necessarily-continuous functions from a topological space
into some type (or type family) form a sheaf.

Why do the continuous functions form a sheaf?
The point is just that continuity is a local condition,
so one can use the lifting condition for functions to provide a candidate lift,
then verify that the lift is actually continuous by using the factorisation condition for the lift
(which guarantees that on each open set it agrees with the functions being lifted,
which were assumed to be continuous).

This file abstracts this argument to work for
any collection of dependent functions on a topological space
satisfying a "local predicate".

As an application, we check that continuity is a local predicate in this sense, and provide
* `TopCat.sheafToTop`: continuous functions into a topological space form a sheaf

A sheaf constructed in this way has a natural map `stalkToFiber` from the stalks
to the types in the ambient type family.

We give conditions sufficient to show that this map is injective and/or surjective.
-/

@[expose] public section

noncomputable section

variable {X : TopCat}
variable (T : X ‚Üí Type*)

open TopologicalSpace

open Opposite

open CategoryTheory

open CategoryTheory.Limits

open CategoryTheory.Limits.Types

namespace TopCat

/-- Given a topological space `X : TopCat` and a type family `T : X ‚Üí Type`,
a `P : PrelocalPredicate T` consists of:
* a family of predicates `P.pred`, one for each `U : Opens X`, of the form `(Œ† x : U, T x) ‚Üí Prop`
* a proof that if `f : Œ† x : V, T x` satisfies the predicate on `V : Opens X`, then
  the restriction of `f` to any open subset `U` also satisfies the predicate.
-/
structure PrelocalPredicate where
  /-- The underlying predicate of a prelocal predicate -/
  pred : Œ† ‚¶ÉU : Opens X‚¶Ñ, (Œ† x : U, T x) ‚Üí Prop
  -- TODO: change `pred` to `Pred` according to naming convention
  /-- The underlying predicate should be invariant under restriction -/
  res : ‚àÄ {U V : Opens X} (i : U ‚ü∂ V) (f : Œ† x : V, T x), pred f ‚Üí pred fun x : U ‚Ü¶ f (i x)

section Sheafify

variable {T} (P : Œ† ‚¶ÉU : Opens X‚¶Ñ, (Œ† x : U, T x) ‚Üí Prop)

/-- The sheafification of a predicate. -/
def Sheafify ‚¶ÉU : Opens X‚¶Ñ (f : Œ† x : U, T x) :=
  ‚àÄ x : U, ‚àÉ (V : Opens X) (_ : x.1 ‚àà V) (i : V ‚ü∂ U), P (f <| i ¬∑)

lemma le_sheafify : P ‚â§ Sheafify P := fun U _f hf x ‚Ü¶ ‚ü®U, x.2, ùüô U, hf‚ü©

/-- A predicate is local if sheafification doesn't make it more general. -/
def IsLocal := Sheafify P ‚â§ P

lemma sheafify_eq_iff : Sheafify P = P ‚Üî IsLocal P := by
  simp_rw [IsLocal, le_antisymm_iff, le_sheafify, and_true]

lemma isLocal_sheafify : IsLocal (Sheafify P) := fun _U _f h x ‚Ü¶
  have ‚ü®_V, m, i, p‚ü© := h x
  have ‚ü®V, m', i', p'‚ü© := p ‚ü®x, m‚ü©
  ‚ü®V, m', i' ‚â´ i, p'‚ü©

end Sheafify

variable (X)

/-- Continuity is a "prelocal" predicate on functions to a fixed topological space `T`.
-/
@[simps!]
def continuousPrelocal (T) [TopologicalSpace T] : PrelocalPredicate fun _ : X ‚Ü¶ T where
  pred {_} f := Continuous f
  res {_ _} i _ h := Continuous.comp h (Opens.isOpenEmbedding_of_le i.le).continuous

/-- Satisfying the inhabited linter. -/
instance inhabitedPrelocalPredicate (T) [TopologicalSpace T] :
    Inhabited (PrelocalPredicate fun _ : X ‚Ü¶ T) :=
  ‚ü®continuousPrelocal X T‚ü©

variable {X} in
/-- Given a topological space `X : TopCat` and a type family `T : X ‚Üí Type`,
a `P : LocalPredicate T` consists of:
* a family of predicates `P.pred`, one for each `U : Opens X`, of the form `(Œ† x : U, T x) ‚Üí Prop`
* a proof that if `f : Œ† x : V, T x` satisfies the predicate on `V : Opens X`, then
  the restriction of `f` to any open subset `U` also satisfies the predicate, and
* a proof that given some `f : Œ† x : U, T x`,
  if for every `x : U` we can find an open set `x ‚àà V ‚â§ U`
  so that the restriction of `f` to `V` satisfies the predicate,
  then `f` itself satisfies the predicate.
-/
structure LocalPredicate extends PrelocalPredicate T where
  /-- A local predicate must be local --- provided that it is locally satisfied, it is also globally
  satisfied -/
  locality : IsLocal pred

section Pullback

variable {X T} {S : X ‚Üí Type*} (F : Œ† x : X, T x ‚Üí S x) (P : Œ† ‚¶ÉU : Opens X‚¶Ñ, (Œ† x : U, S x) ‚Üí Prop)

/-- The pullback of a predicate along a map between type families. -/
def Pullback ‚¶ÉU : Opens X‚¶Ñ (s : Œ† x : U, T x) : Prop := P (F _ <| s ¬∑)

/-- The pullback of a prelocal predicate. -/
def PrelocalPredicate.pullback (P : PrelocalPredicate S) : PrelocalPredicate T where
  pred := Pullback F P.pred
  res i f := P.res i (F _ <| f ¬∑)

/-- The pullback of a local predicate. -/
def LocalPredicate.pullback (P : LocalPredicate S) : LocalPredicate T where
  __ := P.toPrelocalPredicate.pullback F
  locality U s := P.locality U (F _ <| s ¬∑)

end Pullback

section Properties

variable {B : TopCat} {F : B ‚Üí Type*} (P : Œ† ‚¶ÉU : Opens B‚¶Ñ, (Œ† b : U, F b) ‚Üí Prop) (b : B)

/-- The surjectivity criterion for a family of types `F` to behave like the stalks of a
set of sections (represented as a predicate `pred`) says that every germ comes from a section. -/
abbrev IsStalkSurj :=
  ‚àÄ x : F b, ‚àÉ (U : OpenNhds b) (s : Œ† b : U.1, F b), P s ‚àß s ‚ü®b, U.2‚ü© = x

open OpenNhds

/-- The injectivity criterion for a family of types `F` to behave like stalks says that
if two sections pass through the same germ, then they are equal on a neighborhood. -/
abbrev IsStalkInj :=
  ‚àÄ (U V : OpenNhds b) (s : Œ† b : U.1, F b) (t : Œ† b : V.1, F b),
    P s ‚Üí P t ‚Üí s ‚ü®b, U.2‚ü© = t ‚ü®b, V.2‚ü© ‚Üí
    ‚àÉ (W : OpenNhds b) (iU : W ‚ü∂ U) (iV : W ‚ü∂ V), ‚àÄ w : W.1, s (iU w) = t (iV w)

/-- The injectivity criterion suitable for a prelocal predicate. -/
abbrev IsStalkInj' :=
  ‚àÄ (U : OpenNhds b) (s t : Œ† b : U.1, F b), P s ‚Üí P t ‚Üí s ‚ü®b, U.2‚ü© = t ‚ü®b, U.2‚ü© ‚Üí
    ‚àÉ (V : OpenNhds b) (i : V ‚ü∂ U), ‚àÄ b : V.1, s (i b) = t (i b)

theorem IsStalkInj.isStalkInj' {b : B} (h : IsStalkInj P b) : IsStalkInj' P b :=
  fun U s t hs ht eq ‚Ü¶ have ‚ü®W, iU, _, h‚ü© := h U U s t hs ht eq; ‚ü®W, iU, h‚ü©

theorem PrelocalPredicate.isStalkInj_iff {P : PrelocalPredicate F} {b : B} :
    IsStalkInj P.pred b ‚Üî IsStalkInj' P.pred b := by
  refine ‚ü®(¬∑.isStalkInj'), fun h U V s t hs ht eq ‚Ü¶ ?_‚ü©
  have ‚ü®W, i, h‚ü© := h _ _ _ (P.res (infLELeft U V) s hs) (P.res (infLERight U V) t ht) eq
  exact ‚ü®W, i ‚â´ infLELeft U V, i ‚â´ infLERight U V, h‚ü©

/-- A set of sections satisfies the identity principle on an open set `U` if sections on `U`
are determined by any germ. `AnalyticOnNhd.eqOn_of_preconnected_of_eventuallyEq` shows that
sheaves of analytic functions satisfies the identity principle on any connected open set. -/
abbrev HasIdentityPrincipleOn (U : Opens B) :=
  ‚àÄ (s t : Œ† b : U.1, F b) b, P s ‚Üí P t ‚Üí s b = t b ‚Üí s = t

/-- A set of sections satisfies the identity principle at a point `b` if every neighborhood of `b`
contains a neighborhood on which the identity principle is satisfied.
This definition is intended to be applied to a locally connected base space `B`. -/
abbrev HasIdentityPrinciple :=
  ‚àÄ U : OpenNhds b, ‚àÉ V ‚â§ U, HasIdentityPrincipleOn P V.1

/-- A set of sections is separated at a point `b` if any two germs at `b` can be separated by
disjoint sections. -/
abbrev IsSeparated :=
  ‚àÄ x y : F b, x ‚â† y ‚Üí ‚àÉ (U : OpenNhds b) (s t : Œ† b : U.1, F b), P s ‚àß P t ‚àß
    s ‚ü®b, U.2‚ü© = x ‚àß t ‚ü®b, U.2‚ü© = y ‚àß ‚àÄ b' : U.1, s b' ‚â† t b'

/-- A set of sections is constant on a (connected) open set `U` if every germ on `U` can be
extended to a section on `U` in exactly one way.
This corresponds to the `IsEvenlyCovered` condition in the associated √©tale space. -/
abbrev IsConstantOn (U : Opens B) : Prop :=
  ‚àÄ (b‚ÇÄ : U) (x : F b‚ÇÄ), ‚àÉ! s : Œ† b : U, F b, P s ‚àß s b‚ÇÄ = x

/-- A trivialization indexed by `Œπ` of a set of sections on a set `U` is a subset of sections
indexed by `Œπ` which induces, for each point of `U`, a bijection between `Œπ` and the germs
at that point. Together with `IsStalkInj`, this is enough to guarantee that `U` is evenly
covered by the √©tal√© space associated to the set of sections. -/
structure TrivializationOn (U : Opens B) (Œπ : Type*) : Type _ where
  /-- The sections indexed by `Œπ`. -/
  sec : Œπ ‚Üí Œ† b : U, F b
  pred i : P (sec i)
  bijective b : Function.Bijective (sec ¬∑ b)

/-- A set of sections is weakly locally constant at a point `b` if `b` has a neighborhood on which
every germ can be extended to a section in exactly one way.
This is weaker than `IsLocallyConstant`, and the √©tal√© space associated to a weakly locally
constant set of sections is not necessarily a covering space, but `IsWeaklyLocallyConstant` still
implies `IsSeparated` and `IsStalkSurj`. -/
abbrev IsWeaklyLocallyConstant := ‚àÉ U : OpenNhds b, IsConstantOn P U.1

/-- A set of sections is locally constant at a point `b` if every neighborhood of `b` contains
a neighborhood on which every germ can be extended to a section in exactly one way.
This definition is intended to be applied to a locally connected base space `B`:
if `B` is not locally connected, locally constant sheaves on `B` may not give rise to
locally constant sets of sections, but locally constant sets of sections do always give
rise to covering spaces. -/
abbrev IsLocallyConstant := ‚àÄ U : OpenNhds b, ‚àÉ V ‚â§ U, IsConstantOn P V.1

variable {P}

theorem isConstantOn_iff {U : Opens B} : IsConstantOn P U ‚Üî
    HasIdentityPrincipleOn P U ‚àß ‚àÄ (b : U) (x : F b), ‚àÉ s : Œ† b : U, F b, P s ‚àß s b = x where
  mp h := ‚ü®fun _s t b hs ht eq ‚Ü¶ (h b (t b)).unique ‚ü®hs, eq‚ü© ‚ü®ht, rfl‚ü©,
    fun b x ‚Ü¶ (h b x).exists‚ü©
  mpr := fun ‚ü®ip, surj‚ü© b x ‚Ü¶ existsUnique_of_exists_of_unique (surj b x)
    fun s t hs ht ‚Ü¶ ip s t b hs.1 ht.1 (hs.2.trans ht.2.symm)

theorem IsConstantOn.hasIdentityPrincipleOn {U : Opens B} (h : IsConstantOn P U) :
    HasIdentityPrincipleOn P U := (isConstantOn_iff.mp h).1

theorem IsLocallyConstant.hasIdentityPrinciple (h : IsLocallyConstant P b) :
    HasIdentityPrinciple P b :=
  fun U ‚Ü¶ have ‚ü®V, le, hV‚ü© := h U; ‚ü®V, le, hV.hasIdentityPrincipleOn‚ü©

theorem IsLocallyConstant.isWeaklyLocallyConstant (h : IsLocallyConstant P b) :
    IsWeaklyLocallyConstant P b :=
  have ‚ü®U, _, hU‚ü© := h ‚ä§; ‚ü®U, hU‚ü©

theorem HasIdentityPrinciple.isSeparated {P : PrelocalPredicate F}
    (ip : HasIdentityPrinciple P.pred b) (surj : IsStalkSurj P.pred b) : IsSeparated P.pred b :=
  fun x y ne ‚Ü¶ by
    obtain ‚ü®U‚ÇÅ, s‚ÇÅ, h‚ÇÅ, rfl‚ü© := surj x
    obtain ‚ü®U‚ÇÇ, s‚ÇÇ, h‚ÇÇ, rfl‚ü© := surj y
    have ‚ü®U, le, hU‚ü© := ip (U‚ÇÅ ‚äì U‚ÇÇ)
    replace h‚ÇÅ := P.res (le.hom ‚â´ infLELeft ..) _ h‚ÇÅ
    replace h‚ÇÇ := P.res (le.hom ‚â´ infLERight ..) _ h‚ÇÇ
    exact ‚ü®U, _, _, h‚ÇÅ, h‚ÇÇ, rfl, rfl, fun x eq ‚Ü¶ ne <| congr_fun (hU _ _ _ h‚ÇÅ h‚ÇÇ eq) ‚ü®b, U.2‚ü©‚ü©

theorem IsWeaklyLocallyConstant.isStalkSurj (h : IsWeaklyLocallyConstant P b) :
    IsStalkSurj P b :=
  fun x ‚Ü¶ have ‚ü®U, hU‚ü© := h; have ‚ü®s, hs, _‚ü© := hU ‚ü®b, U.2‚ü© x; ‚ü®U, s, hs‚ü©

theorem IsWeaklyLocallyConstant.isSeparated (h : IsWeaklyLocallyConstant P b) :
    IsSeparated P b :=
  fun x y ne ‚Ü¶ by
    have ‚ü®U, hU‚ü© := h
    have ‚ü®s, hs, hsu‚ü© := hU ‚ü®b, U.2‚ü© x
    have ‚ü®t, ht, htu‚ü© := hU ‚ü®b, U.2‚ü© y
    refine ‚ü®U, s, t, hs.1, ht.1, hs.2, ht.2, fun b' eq ‚Ü¶ ne ?_‚ü©
    rw [‚Üê hs.2, (isConstantOn_iff.mp hU).1 s t b' hs.1 ht.1 eq, ht.2]

theorem HasIdentityPrinciple.isStalkInj {P : PrelocalPredicate F}
    (h : HasIdentityPrinciple P.pred b) : IsStalkInj P.pred b :=
  P.isStalkInj_iff.mpr fun U _s _t hs ht eq ‚Ü¶ have ‚ü®V, le, ip‚ü© := h U
    ‚ü®V, le.hom, congr_fun (ip _ _ ‚ü®b, V.2‚ü© (P.res le.hom _ hs) (P.res le.hom _ ht) eq)‚ü©

namespace IsConstantOn

variable {U : Opens B} (h : IsConstantOn P U) {b : U} (x : F b)

/-- The section on `U` with given germ at `b : U` within a set of sections constant on `U`. -/
def sec : Œ† b : U, F b := (h b x).choose

lemma pred_sec : P (h.sec x) := (h b x).choose_spec.1.1

lemma sec_apply : h.sec x b = x := (h b x).choose_spec.1.2

lemma sec_eq_of_pred {s : Œ† b : U, F b} (hs : P s) (b : U) : h.sec (s b) = s :=
  (isConstantOn_iff.mp h).1 _ _ b (h.pred_sec _) hs (h.sec_apply _)

/-- The monodromy induced by a set of sections constant on `U`. -/
def monodromy (b b' : U) : F b ‚âÉ F b' where
  toFun x := h.sec x b'
  invFun x := h.sec x b
  left_inv _ := by simp_rw [h.sec_eq_of_pred (h.pred_sec _), h.sec_apply]
  right_inv _ := by simp_rw [h.sec_eq_of_pred (h.pred_sec _), h.sec_apply]

include h in
/-- The trivialization induced by a set of sections constant on `U`. -/
def trivializationOn (b : U) : TrivializationOn P U (F b) where
  sec := h.sec
  pred := h.pred_sec
  bijective _ := (h.monodromy ..).bijective

end IsConstantOn

namespace TrivializationOn

variable {U : Opens B} {Œπ} (t : TrivializationOn P U Œπ)

/-- A trivialization of a set of sections over a set `U` induces a trivialization of the `Sigma`
type over `U`. -/
def equiv : {f : Œ£ b, F b // f.1 ‚àà U} ‚âÉ U √ó Œπ :=
  .trans (.symm (.trans (.sigmaCongrRight (.ofBijective _ <| t.bijective ¬∑)) .sigmaSubtypeComm))
    (.sigmaEquivProd ..)

lemma symm_apply_coe (x : U √ó Œπ) : t.equiv.symm x = Sigma.mk _ (t.sec x.2 x.1) := rfl

lemma preimage_snd_comp_equiv (i : Œπ) :
    Prod.snd ‚àò t.equiv ‚Åª¬π' {i} = Subtype.val ‚Åª¬π' Set.range (Sigma.mk _ <| t.sec i ¬∑) :=
  Set.ext fun f ‚Ü¶ (Equiv.symm_apply_eq _).trans <| by
    simp only [Equiv.sigmaSubtypeComm, Equiv.coe_fn_symm_mk,
      Equiv.ofBijective_apply, Set.mem_preimage, Set.mem_range]
    refine ‚ü®fun h ‚Ü¶ ‚ü®‚ü®_, f.2‚ü©, congr(Sigma.mk _ $h).symm‚ü©, fun ‚ü®b, h‚ü© ‚Ü¶ ?_‚ü©
    obtain ‚ü®‚ü®b', x‚ü©, hbx‚ü© := f
    cases congr($h.1)
    exact congr($h.2).symm

end TrivializationOn

end Properties

/-- Continuity is a "local" predicate on functions to a fixed topological space `T`.
-/
def continuousLocal (T : Type*) [TopologicalSpace T] : LocalPredicate fun _ : X ‚Ü¶ T :=
  { continuousPrelocal X T with
    locality := fun {U} f w ‚Ü¶ by
      apply continuous_iff_continuousAt.2
      intro x
      rcases w x with ‚ü®V, m, i, w‚ü©
      dsimp at w
      rw [continuous_iff_continuousAt] at w
      specialize w ‚ü®x, m‚ü©
      simpa using (Opens.isOpenEmbedding_of_le i.le).continuousAt_iff.1 w }

/-- Satisfying the inhabited linter. -/
instance inhabitedLocalPredicate (T : Type*) [TopologicalSpace T] :
    Inhabited (LocalPredicate fun _ : X ‚Ü¶ T) :=
  ‚ü®continuousLocal X T‚ü©

variable {X T}

/-- The conjunction of two prelocal predicates is prelocal. -/
def PrelocalPredicate.and (P Q : PrelocalPredicate T) : PrelocalPredicate T where
  pred _ f := P.pred f ‚àß Q.pred f
  res i f h := ‚ü®P.res i f h.1, Q.res i f h.2‚ü©

lemma IsLocal.inf {P Q : Œ† ‚¶ÉU : Opens X‚¶Ñ, (Œ† x : U, T x) ‚Üí Prop} (hP : IsLocal P) (hQ : IsLocal Q) :
    IsLocal (P ‚äì Q) := fun U f w ‚Ü¶ by
  refine ‚ü®hP U f ?_, hQ U f ?_‚ü© <;> (intro x; have ‚ü®V, hV, i, h‚ü© := w x; use V, hV, i)
  exacts [h.1, h.2]

/-- The conjunction of two prelocal predicates is prelocal. -/
def LocalPredicate.and (P Q : LocalPredicate T) : LocalPredicate T where
  __ := P.1.and Q.1
  locality := P.locality.inf Q.locality

/-- The local predicate of being a partial section of a function. -/
def isSection {T} (p : T ‚Üí X) : LocalPredicate fun _ : X ‚Ü¶ T where
  pred _ f := p ‚àò f = (‚Üë)
  res _ _ h := funext fun _ ‚Ü¶ congr_fun h _
  locality _ _ w := funext fun x ‚Ü¶ have ‚ü®_, hV, _, h‚ü© := w x; congr_fun h ‚ü®x, hV‚ü©

/-- Continuity is a local predicate on sections of a map between topological spaces. -/
def isContinuousSection {T} [TopologicalSpace T] (p : T ‚Üí X) : LocalPredicate (p ‚Åª¬π' {¬∑}) where
  pred _U f := Continuous fun x ‚Ü¶ (f x).1
  res i _f := (continuousLocal X T).res i _
  locality U _f h := (continuousLocal X T).locality U _ h

section Sheafify

variable {T : X ‚Üí Type*} (P : PrelocalPredicate T)

/-- Given a `P : PrelocalPredicate`, we can always construct a `LocalPredicate`
by asking that the condition from `P` holds locally near every point.
-/
def PrelocalPredicate.sheafify : LocalPredicate T where
  pred := Sheafify P.pred
  res {V U} i f w x := by
    rcases w (i x) with ‚ü®V', m', i', p‚ü©
    exact ‚ü®V ‚äì V', ‚ü®x.2, m'‚ü©, V.infLELeft _, P.res (V.infLERight V') _ p‚ü©
  locality := isLocal_sheafify _

variable {P}

theorem PrelocalPredicate.sheafifyOf {U : Opens X} {f : Œ† x : U, T x} (h : P.pred f) :
    P.sheafify.pred f := le_sheafify _ _ _ h

theorem IsStalkSurj.sheafify {x : X} (h : IsStalkSurj P.pred x) :
    IsStalkSurj P.sheafify.pred x :=
  fun t ‚Ü¶ have ‚ü®U, s, hs, eq‚ü© := h t; ‚ü®U, s, P.sheafifyOf hs, eq‚ü©

theorem IsStalkInj.sheafify {x : X} (h : IsStalkInj P.pred x) : IsStalkInj P.sheafify.pred x :=
  fun U V _fU _fV hU hV e ‚Ü¶
    have ‚ü®U', mU, iU, hU‚ü© := hU ‚ü®x, U.2‚ü©
    have ‚ü®V', mV, iV, hV‚ü© := hV ‚ü®x, V.2‚ü©
    have ‚ü®W, iU', iV', h‚ü© := h ‚ü®U', mU‚ü© ‚ü®V', mV‚ü© _ _ hU hV e
    ‚ü®W, iU' ‚â´ iU, iV' ‚â´ iV, h‚ü©

end Sheafify

namespace PrelocalPredicate

/-- For a unary operation (e.g. `x ‚Ü¶ -x`) defined at each stalk, if a prelocal predicate is closed
under the operation on each open set (possibly by refinement), then the sheafified predicate is
also closed under the operation. See `sheafify_inductionOn'` for the version without refinement. -/
theorem sheafify_inductionOn {X : TopCat} {T : X ‚Üí Type*} (P : PrelocalPredicate T)
    (op : {x : X} ‚Üí T x ‚Üí T x)
    (hop : ‚àÄ {U : Opens X} {a : (x : U) ‚Üí T x}, P.pred a ‚Üí
      ‚àÄ (p : U), ‚àÉ (W : Opens X) (i : W ‚ü∂ U), p.1 ‚àà W ‚àß P.pred fun x : W ‚Ü¶ op (a (i x)))
    {U : Opens X} {a : (x : U) ‚Üí T x} (ha : P.sheafify.pred a) :
    P.sheafify.pred (fun x : U ‚Ü¶ op (a x)) := by
  intro x
  rcases ha x with ‚ü®Va, ma, ia, ha‚ü©
  rcases hop ha ‚ü®x, ma‚ü© with ‚ü®W, sa, hx, hw‚ü©
  exact ‚ü®W, hx, sa ‚â´ ia, hw‚ü©

/-- For a unary operation (e.g. `x ‚Ü¶ -x`) defined at each stalk, if a prelocal predicate is closed
under the operation on each open set, then the sheafified predicate is also closed under the
operation. See `sheafify_inductionOn` for the version with refinement. -/
theorem sheafify_inductionOn' {X : TopCat} {T : X ‚Üí Type*} (P : PrelocalPredicate T)
    (op : {x : X} ‚Üí T x ‚Üí T x)
    (hop : ‚àÄ {U : Opens X} {a : (x : U) ‚Üí T x}, P.pred a ‚Üí P.pred fun x : U ‚Ü¶ op (a x))
    {U : Opens X} {a : (x : U) ‚Üí T x} (ha : P.sheafify.pred a) :
    P.sheafify.pred (fun x : U ‚Ü¶ op (a x)) :=
  P.sheafify_inductionOn op (fun ha p ‚Ü¶ ‚ü®_, ùüô _, p.2, hop ha‚ü©) ha

/-- For a binary operation (e.g. `x ‚Ü¶ y ‚Ü¶ x + y`) defined at each stalk, if a prelocal predicate is
closed under the operation on each open set (possibly by refinement), then the sheafified predicate
is also closed under the operation. See `sheafify_inductionOn‚ÇÇ'` for the version without
refinement. -/
theorem sheafify_inductionOn‚ÇÇ {X : TopCat} {T‚ÇÅ T‚ÇÇ T‚ÇÉ : X ‚Üí Type*}
    (P‚ÇÅ : PrelocalPredicate T‚ÇÅ) (P‚ÇÇ : PrelocalPredicate T‚ÇÇ) (P‚ÇÉ : PrelocalPredicate T‚ÇÉ)
    (op : {x : X} ‚Üí T‚ÇÅ x ‚Üí T‚ÇÇ x ‚Üí T‚ÇÉ x)
    (hop : ‚àÄ {U V : Opens X} {a : (x : U) ‚Üí T‚ÇÅ x} {b : (x : V) ‚Üí T‚ÇÇ x}, P‚ÇÅ.pred a ‚Üí P‚ÇÇ.pred b ‚Üí
      ‚àÄ (p : (U ‚äì V : Opens X)), ‚àÉ (W : Opens X) (ia : W ‚ü∂ U) (ib : W ‚ü∂ V),
      p.1 ‚àà W ‚àß P‚ÇÉ.pred fun x : W ‚Ü¶ op (a (ia x)) (b (ib x)))
    {U : Opens X} {a : (x : U) ‚Üí T‚ÇÅ x} {b : (x : U) ‚Üí T‚ÇÇ x}
    (ha : P‚ÇÅ.sheafify.pred a) (hb : P‚ÇÇ.sheafify.pred b) :
    P‚ÇÉ.sheafify.pred (fun x : U ‚Ü¶ op (a x) (b x)) := by
  intro x
  rcases ha x with ‚ü®Va, ma, ia, ha‚ü©
  rcases hb x with ‚ü®Vb, mb, ib, hb‚ü©
  rcases hop ha hb ‚ü®x, ma, mb‚ü© with ‚ü®W, sa, sb, hx, hw‚ü©
  exact ‚ü®W, hx, sa ‚â´ ia, hw‚ü©

/-- For a binary operation (e.g. `x ‚Ü¶ y ‚Ü¶ x + y`) defined at each stalk, if a prelocal predicate is
closed under the operation on each open set, then the sheafified predicate is also closed under the
operation. See `sheafify_inductionOn‚ÇÇ` for the version with refinement. -/
theorem sheafify_inductionOn‚ÇÇ' {X : TopCat} {T‚ÇÅ T‚ÇÇ T‚ÇÉ : X ‚Üí Type*}
    (P‚ÇÅ : PrelocalPredicate T‚ÇÅ) (P‚ÇÇ : PrelocalPredicate T‚ÇÇ) (P‚ÇÉ : PrelocalPredicate T‚ÇÉ)
    (op : {x : X} ‚Üí T‚ÇÅ x ‚Üí T‚ÇÇ x ‚Üí T‚ÇÉ x)
    (hop : ‚àÄ {U V : Opens X} {a : (x : U) ‚Üí T‚ÇÅ x} {b : (x : V) ‚Üí T‚ÇÇ x}, P‚ÇÅ.pred a ‚Üí P‚ÇÇ.pred b ‚Üí
      P‚ÇÉ.pred fun x : (U ‚äì V : Opens X) ‚Ü¶ op (a ‚ü®x, x.2.1‚ü©) (b ‚ü®x, x.2.2‚ü©))
    {U : Opens X} {a : (x : U) ‚Üí T‚ÇÅ x} {b : (x : U) ‚Üí T‚ÇÇ x}
    (ha : P‚ÇÅ.sheafify.pred a) (hb : P‚ÇÇ.sheafify.pred b) :
    P‚ÇÉ.sheafify.pred (fun x : U ‚Ü¶ op (a x) (b x)) :=
  P‚ÇÅ.sheafify_inductionOn‚ÇÇ P‚ÇÇ P‚ÇÉ op
    (fun ha hb p ‚Ü¶ ‚ü®_, Opens.infLELeft _ _, Opens.infLERight _ _, p.2, hop ha hb‚ü©) ha hb

end PrelocalPredicate

/-- The subpresheaf of dependent functions on `X` satisfying the "pre-local" predicate `P`.
-/
@[simps!]
def subpresheafToTypes (P : PrelocalPredicate T) : Presheaf (Type _) X where
  obj U := { f : Œ† x : U.unop, T x // P.pred f }
  map {_ _} i f := ‚ü®fun x ‚Ü¶ f.1 (i.unop x), P.res i.unop f.1 f.2‚ü©

namespace subpresheafToTypes

variable (P : PrelocalPredicate T)

/-- The natural transformation including the subpresheaf of functions satisfying a local predicate
into the presheaf of all functions.
-/
def subtype : subpresheafToTypes P ‚ü∂ presheafToTypes X T where app _ f := f.1

open TopCat.Presheaf

attribute [local instance] Types.instFunLike Types.instConcreteCategory in
/-- The functions satisfying a local predicate satisfy the sheaf condition.
-/
theorem isSheaf (P : LocalPredicate T) : (subpresheafToTypes P.toPrelocalPredicate).IsSheaf :=
  Presheaf.isSheaf_of_isSheafUniqueGluing_types _ fun Œπ U sf sf_comp ‚Ü¶ by
    -- We show the sheaf condition in terms of unique gluing.
    -- First we obtain a family of sections for the underlying sheaf of functions,
    -- by forgetting that the predicate holds
    let sf' (i : Œπ) : (presheafToTypes X T).obj (op (U i)) := (sf i).val
    -- Since our original family is compatible, this one is as well
    have sf'_comp : (presheafToTypes X T).IsCompatible U sf' := fun i j ‚Ü¶
      congr_arg Subtype.val (sf_comp i j)
    -- So, we can obtain a unique gluing
    obtain ‚ü®gl, gl_spec, gl_uniq‚ü© := (sheafToTypes X T).existsUnique_gluing U sf'
      -- `by exact` to help Lean infer the `ConcreteCategory` instance
      (by exact sf'_comp)
    refine ‚ü®‚ü®gl, ?_‚ü©, ?_, ?_‚ü©
    ¬∑ -- Our first goal is to show that this chosen gluing satisfies the
      -- predicate. Of course, we use locality of the predicate.
      apply P.locality
      rintro ‚ü®x, mem‚ü©
      -- Once we're at a particular point `x`, we can select some open set `x ‚àà U i`.
      choose i hi using Opens.mem_iSup.mp mem
      -- We claim that the predicate holds in `U i`
      use U i, hi, Opens.leSupr U i
      -- This follows, since our original family `sf` satisfies the predicate
      convert (sf i).property using 1
      exact gl_spec i
    -- It remains to show that the chosen lift is really a gluing for the subsheaf and
    -- that it is unique. Both of which follow immediately from the corresponding facts
    -- in the sheaf of functions without the local predicate.
    ¬∑ exact fun i ‚Ü¶ Subtype.ext (gl_spec i)
    ¬∑ intro gl' hgl'
      refine Subtype.ext ?_
      exact gl_uniq gl'.1 fun i ‚Ü¶ congr_arg Subtype.val (hgl' i)

end subpresheafToTypes

/-- The subsheaf of the sheaf of all dependently typed functions satisfying the local predicate `P`.
-/
@[simps]
def subsheafToTypes (P : LocalPredicate T) : Sheaf (Type _) X :=
  ‚ü®subpresheafToTypes P.toPrelocalPredicate, subpresheafToTypes.isSheaf P‚ü©

/-- There is a canonical map from the stalk to the original fiber, given by evaluating sections. -/
def stalkToFiber (P : PrelocalPredicate T) (x : X) : (subpresheafToTypes P).stalk x ‚Üí T x :=
  ULift.down ‚àò colimit.desc _
    { pt := ULift (T x)
      Œπ := { app := fun U f ‚Ü¶ ‚ü®by exact f.1 ‚ü®x, (unop U).2‚ü©‚ü© } }

theorem stalkToFiber_germ (P : PrelocalPredicate T) (U : Opens X) (x : X) (hx : x ‚àà U) (f) :
    stalkToFiber P x ((subpresheafToTypes P).germ U x hx f) = f.1 ‚ü®x, hx‚ü© := by
  simp [stalkToFiber, Presheaf.germ, Colimit.Œπ_desc_apply]

/-- The `stalkToFiber` map is surjective at `x` if
every point in the fiber `T x` has an allowed section passing through it. -/
-- TODO: upgrade to iff?
theorem stalkToFiber_surjective (P : PrelocalPredicate T) (x : X) (w : IsStalkSurj P.pred x) :
    Function.Surjective (stalkToFiber P x) := fun t ‚Ü¶ by
  rcases w t with ‚ü®U, f, h, rfl‚ü©
  exact ‚ü®_, stalkToFiber_germ P U.1 x U.2 ‚ü®f, h‚ü©‚ü©

/-- The `stalkToFiber` map is injective at `x` if any two allowed sections which agree at `x`
agree on some neighborhood of `x`. -/
-- TODO: upgrade to iff?
theorem stalkToFiber_injective (P : PrelocalPredicate T) (x : X) (w : IsStalkInj P.pred x) :
    Function.Injective (stalkToFiber P x) := fun tU tV h ‚Ü¶ by
  -- We promise to provide all the ingredients of the proof later:
  let Q :
    ‚àÉ (W : (OpenNhds x)·µí·µñ) (s : Œ† w : (unop W).1, T w) (hW : P.pred s),
      tU = (subpresheafToTypes P).germ _ x (unop W).2 ‚ü®s, hW‚ü© ‚àß
        tV = (subpresheafToTypes P).germ _ x (unop W).2 ‚ü®s, hW‚ü© := ?_
  ¬∑ choose W s hW e using Q
    exact e.1.trans e.2.symm
  -- Then use induction to pick particular representatives of `tU tV : stalk x`
  obtain ‚ü®U, ‚ü®fU, hU‚ü©, rfl‚ü© := jointly_surjective' tU
  obtain ‚ü®V, ‚ü®fV, hV‚ü©, rfl‚ü© := jointly_surjective' tV
  -- Decompose everything into its constituent parts:
  simp_rw [stalkToFiber, Function.comp_apply, Colimit.Œπ_desc_apply] at h
  specialize w (unop U) (unop V) fU fV hU hV h
  rcases w with ‚ü®W, iU, iV, w‚ü©
  -- and put it back together again in the correct order.
  refine ‚ü®op W, fun w ‚Ü¶ fU (iU w : (unop U).1), P.res ?_ _ hU, ?_‚ü©
  ¬∑ rcases W with ‚ü®W, m‚ü©
    exact iU
  ¬∑ exact ‚ü®colimit_sound iU.op (Subtype.ext rfl), colimit_sound iV.op (Subtype.ext (funext w).symm)‚ü©

universe u

/-- Some repackaging:
the presheaf of functions satisfying `continuousPrelocal` is just the same thing as
the presheaf of continuous functions.
-/
def subpresheafContinuousPrelocalIsoPresheafToTop {X : TopCat.{u}} (T : TopCat.{u}) :
    subpresheafToTypes (continuousPrelocal X T) ‚âÖ presheafToTop X T :=
  NatIso.ofComponents fun X ‚Ü¶
    { hom := by rintro ‚ü®f, c‚ü©; exact ofHom ‚ü®f, c‚ü©
      inv := by rintro ‚ü®f, c‚ü©; exact ‚ü®f, c‚ü© }

/-- The sheaf of continuous functions on `X` with values in a space `T`.
-/
def sheafToTop (T : TopCat) : Sheaf (Type _) X :=
  ‚ü®presheafToTop X T,
    Presheaf.isSheaf_of_iso (subpresheafContinuousPrelocalIsoPresheafToTop T)
      (subpresheafToTypes.isSheaf (continuousLocal X T))‚ü©

end TopCat
