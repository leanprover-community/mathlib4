/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.Topology.Sheaves.Presheaf
import Mathlib.CategoryTheory.Sites.Sheaf
import Mathlib.CategoryTheory.Sites.Spaces

/-!
# Sheaves

We define sheaves on a topological space, with values in an arbitrary category.

A presheaf on a topological space `X` is a sheaf precisely when it is a sheaf under the
Grothendieck topology on `opens X`, which expands out to say: For each open cover `{ U·µ¢ }` of
`U`, and a family of compatible functions `A ‚ü∂ F(U·µ¢)` for an `A : X`, there exists a unique
gluing `A ‚ü∂ F(U)` compatible with the restriction.

See the docstring of `TopCat.Presheaf.IsSheaf` for an explanation on the design decisions and a list
of equivalent conditions.

We provide the instance `CategoryTheory.Category (TopCat.Sheaf C X)` as the full subcategory of
presheaves, and the fully faithful functor `Sheaf.forget : TopCat.Sheaf C X ‚•§ TopCat.Presheaf C X`.

-/


universe w v u

noncomputable section

open CategoryTheory CategoryTheory.Limits TopologicalSpace Opposite TopologicalSpace.Opens

namespace TopCat

variable {C : Type u} [Category.{v} C]
variable {X : TopCat.{w}} (F : Presheaf C X) {Œπ : Type v} (U : Œπ ‚Üí Opens X)

namespace Presheaf

/-- The sheaf condition has several different equivalent formulations.
The official definition chosen here is in terms of Grothendieck topologies so that the results on
sites could be applied here easily, and this condition does not require additional constraints on
the value category.
The equivalent formulations of the sheaf condition on `presheaf C X` are as follows :

1. `TopCat.Presheaf.IsSheaf`: (the official definition)
  It is a sheaf with respect to the Grothendieck topology on `opens X`, which is to say:
  For each open cover `{ U·µ¢ }` of `U`, and a family of compatible functions `A ‚ü∂ F(U·µ¢)` for an
  `A : X`, there exists a unique gluing `A ‚ü∂ F(U)` compatible with the restriction.

2. `TopCat.Presheaf.IsSheafEqualizerProducts`: (requires `C` to have all products)
  For each open cover `{ U·µ¢ }` of `U`, `F(U) ‚ü∂ ‚àè·∂ú F(U·µ¢)` is the equalizer of the two morphisms
  `‚àè·∂ú F(U·µ¢) ‚ü∂ ‚àè·∂ú F(U·µ¢ ‚à© U‚±º)`.
  See `TopCat.Presheaf.isSheaf_iff_isSheafEqualizerProducts`.

3. `TopCat.Presheaf.IsSheafOpensLeCover`:
  For each open cover `{ U·µ¢ }` of `U`, `F(U)` is the limit of the diagram consisting of arrows
  `F(V‚ÇÅ) ‚ü∂ F(V‚ÇÇ)` for every pair of open sets `V‚ÇÅ ‚äá V‚ÇÇ` that are contained in some `U·µ¢`.
  See `TopCat.Presheaf.isSheaf_iff_isSheafOpensLeCover`.

4. `TopCat.Presheaf.IsSheafPairwiseIntersections`:
  For each open cover `{ U·µ¢ }` of `U`, `F(U)` is the limit of the diagram consisting of arrows
  from `F(U·µ¢)` and `F(U‚±º)` to `F(U·µ¢ ‚à© U‚±º)` for each pair `(i, j)`.
  See `TopCat.Presheaf.isSheaf_iff_isSheafPairwiseIntersections`.

The following requires `C` to be concrete and complete, and `forget C` to reflect isomorphisms and
preserve limits. This applies to most "algebraic" categories, e.g. groups, abelian groups and rings.

5. `TopCat.Presheaf.IsSheafUniqueGluing`:
  (requires `C` to be concrete and complete; `forget C` to reflect isomorphisms and preserve limits)
  For each open cover `{ U·µ¢ }` of `U`, and a compatible family of elements `x : F(U·µ¢)`, there exists
  a unique gluing `x : F(U)` that restricts to the given elements.
  See `TopCat.Presheaf.isSheaf_iff_isSheafUniqueGluing`.

6. The underlying sheaf of types is a sheaf.
  See `TopCat.Presheaf.isSheaf_iff_isSheaf_comp` and
  `CategoryTheory.Presheaf.isSheaf_iff_isSheaf_forget`.
-/
nonrec def IsSheaf (F : Presheaf.{w, v, u} C X) : Prop :=
  Presheaf.IsSheaf (Opens.grothendieckTopology X) F

/-- The presheaf valued in `Unit` over any topological space is a sheaf.
-/
theorem isSheaf_unit (F : Presheaf (CategoryTheory.Discrete Unit) X) : F.IsSheaf :=
  fun x U S _ x _ => ‚ü®eqToHom (Subsingleton.elim _ _), by cat_disch, fun _ => by cat_disch‚ü©

theorem isSheaf_iso_iff {F G : Presheaf C X} (Œ± : F ‚âÖ G) : F.IsSheaf ‚Üî G.IsSheaf :=
  Presheaf.isSheaf_of_iso_iff Œ±

/-- Transfer the sheaf condition across an isomorphism of presheaves.
-/
theorem isSheaf_of_iso {F G : Presheaf C X} (Œ± : F ‚âÖ G) (h : F.IsSheaf) : G.IsSheaf :=
  (isSheaf_iso_iff Œ±).1 h

end Presheaf

variable (C X)

/-- A `TopCat.Sheaf C X` is a presheaf of objects from `C` over a (bundled) topological space `X`,
satisfying the sheaf condition.
-/
nonrec def Sheaf : Type max u v w :=
  Sheaf (Opens.grothendieckTopology X) C
deriving Category

variable {C X}

/-- The underlying presheaf of a sheaf -/
abbrev Sheaf.presheaf (F : X.Sheaf C) : TopCat.Presheaf C X :=
  F.1

variable (C X)

-- Let's construct a trivial example, to keep the inhabited linter happy.
instance sheafInhabited : Inhabited (Sheaf (CategoryTheory.Discrete PUnit) X) :=
  ‚ü®‚ü®Functor.star _, Presheaf.isSheaf_unit _‚ü©‚ü©

namespace Sheaf

/-- The forgetful functor from sheaves to presheaves.
-/
def forget : TopCat.Sheaf C X ‚•§ TopCat.Presheaf C X :=
  sheafToPresheaf _ _

-- The following instances should be constructed by a deriving handler.
-- https://github.com/leanprover-community/mathlib4/issues/380

instance forget_full : (forget C X).Full where
  map_surjective f := ‚ü®Sheaf.Hom.mk f, rfl‚ü©

instance forgetFaithful : (forget C X).Faithful where
  map_injective := Sheaf.Hom.ext

-- Note: These can be proved by simp.
theorem id_app (F : Sheaf C X) (t) : (ùüô F : F ‚ü∂ F).1.app t = ùüô _ :=
  rfl

theorem comp_app {F G H : Sheaf C X} (f : F ‚ü∂ G) (g : G ‚ü∂ H) (t) :
    (f ‚â´ g).1.app t = f.1.app t ‚â´ g.1.app t :=
  rfl

end Sheaf

lemma Presheaf.IsSheaf.section_ext {X : TopCat.{u}}
    {A : Type*} [Category.{u} A] {FC : A ‚Üí A ‚Üí Type*} {CC : A ‚Üí Type u}
    [‚àÄ X Y : A, FunLike (FC X Y) (CC X) (CC Y)] [ConcreteCategory.{u} A FC]
    [HasLimits A] [PreservesLimits (forget A)] [(forget A).ReflectsIsomorphisms]
    {F : TopCat.Presheaf A X} (hF : TopCat.Presheaf.IsSheaf F)
    {U : (Opens X)·µí·µñ} {s t : ToType (F.obj U)}
    (hst : ‚àÄ x ‚àà U.unop, ‚àÉ V, ‚àÉ hV : V ‚â§ U.unop, x ‚àà V ‚àß
      F.map (homOfLE hV).op s = F.map (homOfLE hV).op t) :
    s = t := by
  have := (isSheaf_iff_isSheaf_of_type _ _).mp
    ((Presheaf.isSheaf_iff_isSheaf_forget (C := Opens X) (A' := A) _ F (forget _)).mp hF)
  choose V hV hxV H using fun x : U.unop ‚Ü¶ hst x.1 x.2
  refine (this.isSheafFor _ (.ofArrows V fun x ‚Ü¶ homOfLE (hV x)) ?_).isSeparatedFor.ext ?_
  ¬∑ exact fun x hx ‚Ü¶ ‚ü®V ‚ü®x, hx‚ü©, homOfLE (hV _), Sieve.le_generate _ _ (.mk _), hxV _‚ü©
  ¬∑ rintro _ _ ‚ü®x‚ü©; exact H x

end TopCat
