/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Scott Morrison
-/
import Mathlib.Topology.Sheaves.PresheafOfFunctions
import Mathlib.Topology.Sheaves.SheafCondition.UniqueGluing

#align_import topology.sheaves.sheaf_of_functions from "leanprover-community/mathlib"@"5dc6092d09e5e489106865241986f7f2ad28d4c8"

/-!
# Sheaf conditions for presheaves of (continuous) functions.

We show that
* `Top.Presheaf.toType_isSheaf`: not-necessarily-continuous functions into a type form a sheaf
* `Top.Presheaf.toTypes_isSheaf`: in fact, these may be dependent functions into a type family

For
* `Top.sheafToTop`: continuous functions into a topological space form a sheaf
please see `Topology/Sheaves/LocalPredicate.lean`, where we set up a general framework
for constructing sub(pre)sheaves of the sheaf of dependent functions.

## Future work
Obviously there's more to do:
* sections of a fiber bundle
* various classes of smooth and structure preserving functions
* functions into spaces with algebraic structure, which the sections inherit
-/


open CategoryTheory Limits TopologicalSpace Opens

universe u

noncomputable section

variable (X : TopCat.{u})

open TopCat

namespace TopCat.Presheaf

/-- We show that the presheaf of functions to a type `T`
(no continuity assumptions, just plain functions)
form a sheaf.

In fact, the proof is identical when we do this for dependent functions to a type family `T`,
so we do the more general case.
-/
theorem toTypes_isSheaf (T : X ‚Üí Type u) : (presheafToTypes X T).IsSheaf :=
  isSheaf_of_isSheafUniqueGluing_types.{u} _ fun Œπ U sf hsf => by
  -- We use the sheaf condition in terms of unique gluing
  -- U is a family of open sets, indexed by `Œπ` and `sf` is a compatible family of sections.
  -- In the informal comments below, I'll just write `U` to represent the union.
    -- Our first goal is to define a function "lifted" to all of `U`.
    -- We do this one point at a time. Using the axiom of choice, we can pick for each
    -- `x : ‚Üë(iSup U)` an index `i : Œπ` such that `x` lies in `U i`
    choose index index_spec using fun x : ‚Üë(iSup U) => Opens.mem_iSup.mp x.2
    -- ‚ä¢ ‚àÉ! s, IsGluing (presheafToTypes X T) U sf s
    -- Using this data, we can glue our functions together to a single section
    let s : ‚àÄ x : ‚Üë(iSup U), T x := fun x => sf (index x) ‚ü®x.1, index_spec x‚ü©
    -- ‚ä¢ ‚àÉ! s, IsGluing (presheafToTypes X T) U sf s
    refine' ‚ü®s, _, _‚ü©
    -- ‚ä¢ (fun s => IsGluing (presheafToTypes X T) U sf s) s
    ¬∑ intro i
      -- ‚ä¢ ‚Üë((presheafToTypes X T).map (leSupr U i).op) s = sf i
      funext x
      -- ‚ä¢ ‚Üë((presheafToTypes X T).map (leSupr U i).op) s x = sf i x
      -- Now we need to verify that this lifted function restricts correctly to each set `U i`.
      -- Of course, the difficulty is that at any given point `x ‚àà U i`,
      -- we may have used the axiom of choice to pick a different `j` with `x ‚àà U j`
      -- when defining the function.
      -- Thus we'll need to use the fact that the restrictions are compatible.
      exact congr_fun (hsf (index ‚ü®x, _‚ü©) i) ‚ü®x, ‚ü®index_spec ‚ü®x.1, _‚ü©, x.2‚ü©‚ü©
      -- üéâ no goals
    ¬∑ -- Now we just need to check that the lift we picked was the only possible one.
      -- So we suppose we had some other gluing `t` of our sections
      intro t ht
      -- ‚ä¢ t = s
      -- and observe that we need to check that it agrees with our choice
      -- for each `x ‚àà ‚Üë(iSup U)`.
      funext x
      -- ‚ä¢ t x = s x
      exact congr_fun (ht (index x)) ‚ü®x.1, index_spec x‚ü©
      -- üéâ no goals
set_option linter.uppercaseLean3 false
#align Top.presheaf.to_Types_is_sheaf TopCat.Presheaf.toTypes_isSheaf

-- We verify that the non-dependent version is an immediate consequence:
/-- The presheaf of not-necessarily-continuous functions to
a target type `T` satsifies the sheaf condition.
-/
theorem toType_isSheaf (T : Type u) : (presheafToType X T).IsSheaf :=
  toTypes_isSheaf X fun _ => T
#align Top.presheaf.to_Type_is_sheaf TopCat.Presheaf.toType_isSheaf

end TopCat.Presheaf

namespace TopCat

/-- The sheaf of not-necessarily-continuous functions on `X` with values in type family
`T : X ‚Üí Type u`.
-/
def sheafToTypes (T : X ‚Üí Type u) : Sheaf (Type u) X :=
  ‚ü®presheafToTypes X T, Presheaf.toTypes_isSheaf _ _‚ü©
set_option linter.uppercaseLean3 false
#align Top.sheaf_to_Types TopCat.sheafToTypes

/-- The sheaf of not-necessarily-continuous functions on `X` with values in a type `T`.
-/
def sheafToType (T : Type u) : Sheaf (Type u) X :=
  ‚ü®presheafToType X T, Presheaf.toType_isSheaf _ _‚ü©
set_option linter.uppercaseLean3 false
#align Top.sheafToType TopCat.sheafToType

end TopCat
