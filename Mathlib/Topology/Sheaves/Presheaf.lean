/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Mario Carneiro, Reid Barton, Andrew Yang
-/
import Mathlib.Topology.Category.TopCat.Opens
import Mathlib.CategoryTheory.Adjunction.Opposites
import Mathlib.CategoryTheory.Adjunction.Unique
import Mathlib.CategoryTheory.Functor.KanExtension.Adjunction
import Mathlib.Topology.Sheaves.Init
import Mathlib.Data.Set.Subsingleton

#align_import topology.sheaves.presheaf from "leanprover-community/mathlib"@"5dc6092d09e5e489106865241986f7f2ad28d4c8"

/-!
# Presheaves on a topological space

We define `TopCat.Presheaf C X` simply as `(TopologicalSpace.Opens X)·µí·µñ ‚•§ C`,
and inherit the category structure with natural transformations as morphisms.

We define
* Given `{X Y : TopCat.{w}}` and `f : X ‚ü∂ Y`, we define
`TopCat.Presheaf.pushforward C f : X.Presheaf C ‚•§ Y.Presheaf C`,
with notation `f _* ‚Ñ±` for `‚Ñ± : X.Presheaf C`.
and for `‚Ñ± : X.Presheaf C` provide the natural isomorphisms
* `TopCat.Presheaf.Pushforward.id : (ùüô X) _* ‚Ñ± ‚âÖ ‚Ñ±`
* `TopCat.Presheaf.Pushforward.comp : (f ‚â´ g) _* ‚Ñ± ‚âÖ g _* (f _* ‚Ñ±)`
along with their `@[simp]` lemmas.

We also define the functors `pullback C f : Y.Presheaf C ‚•§ X.Presheaf c`,
and provide their adjunction at
`TopCat.Presheaf.pushforwardPullbackAdjunction`.
-/

set_option autoImplicit true


universe w v u

open CategoryTheory TopologicalSpace Opposite

variable (C : Type u) [Category.{v} C]

namespace TopCat

/-- The category of `C`-valued presheaves on a (bundled) topological space `X`. -/
-- Porting note(#5171): was @[nolint has_nonempty_instance]
def Presheaf (X : TopCat.{w}) : Type max u v w :=
  (Opens X)·µí·µñ ‚•§ C
set_option linter.uppercaseLean3 false in
#align Top.presheaf TopCat.Presheaf

instance (X : TopCat.{w}) : Category (Presheaf.{w, v, u} C X) :=
  inferInstanceAs (Category ((Opens X)·µí·µñ ‚•§ C : Type max u v w))

variable {C}

namespace Presheaf

@[simp] theorem comp_app {P Q R : Presheaf C X} (f : P ‚ü∂ Q) (g : Q ‚ü∂ R) :
    (f ‚â´ g).app U = f.app U ‚â´ g.app U := rfl

-- Porting note (#10756): added an `ext` lemma,
-- since `NatTrans.ext` can not see through the definition of `Presheaf`.
-- See https://github.com/leanprover-community/mathlib4/issues/5229
@[ext]
lemma ext {P Q : Presheaf C X} {f g : P ‚ü∂ Q} (w : ‚àÄ U : Opens X, f.app (op U) = g.app (op U)) :
    f = g := by
  apply NatTrans.ext
  ext U
  induction U with | _ U => ?_
  apply w

attribute [local instance] CategoryTheory.ConcreteCategory.hasCoeToSort
  CategoryTheory.ConcreteCategory.instFunLike

/-- attribute `sheaf_restrict` to mark lemmas related to restricting sheaves -/
macro "sheaf_restrict" : attr =>
  `(attr|aesop safe 50 apply (rule_sets := [$(Lean.mkIdent `Restrict):ident]))

attribute [sheaf_restrict] bot_le le_top le_refl inf_le_left inf_le_right
  le_sup_left le_sup_right

/-- `restrict_tac` solves relations among subsets (copied from `aesop cat`) -/
macro (name := restrict_tac) "restrict_tac" c:Aesop.tactic_clause* : tactic =>
`(tactic| first | assumption |
  aesop $c*
    (config := { terminal := true
                 assumptionTransparency := .reducible
                 enableSimp := false })
    (rule_sets := [-default, -builtin, $(Lean.mkIdent `Restrict):ident]))

/-- `restrict_tac?` passes along `Try this` from `aesop` -/
macro (name := restrict_tac?) "restrict_tac?" c:Aesop.tactic_clause* : tactic =>
`(tactic|
  aesop? $c*
    (config := { terminal := true
                 assumptionTransparency := .reducible
                 enableSimp := false
                 maxRuleApplications := 300 })
  (rule_sets := [-default, -builtin, $(Lean.mkIdent `Restrict):ident]))

attribute[aesop 10% (rule_sets := [Restrict])] le_trans
attribute[aesop safe destruct (rule_sets := [Restrict])] Eq.trans_le
attribute[aesop safe -50 (rule_sets := [Restrict])] Aesop.BuiltinRules.assumption

example {X} [CompleteLattice X] (v : Nat ‚Üí X) (w x y z : X) (e : v 0 = v 1) (_ : v 1 = v 2)
    (h‚ÇÄ : v 1 ‚â§ x) (_ : x ‚â§ z ‚äì w) (h‚ÇÇ : x ‚â§ y ‚äì z) : v 0 ‚â§ y := by
  restrict_tac

/-- The restriction of a section along an inclusion of open sets.
For `x : F.obj (op V)`, we provide the notation `x |_‚Çï i` (`h` stands for `hom`) for `i : U ‚ü∂ V`,
and the notation `x |_‚Çó U ‚ü™i‚ü´` (`l` stands for `le`) for `i : U ‚â§ V`.
-/
def restrict {X : TopCat} {C : Type*} [Category C] [ConcreteCategory C] {F : X.Presheaf C}
    {V : Opens X} (x : F.obj (op V)) {U : Opens X} (h : U ‚ü∂ V) : F.obj (op U) :=
  F.map h.op x
set_option linter.uppercaseLean3 false in
#align Top.presheaf.restrict TopCat.Presheaf.restrict

/-- restriction of a section along an inclusion -/
scoped[AlgebraicGeometry] infixl:80 " |_‚Çï " => TopCat.Presheaf.restrict
/-- restriction of a section along a subset relation -/
scoped[AlgebraicGeometry] notation:80 x " |_‚Çó " U " ‚ü™" e "‚ü´ " =>
  @TopCat.Presheaf.restrict _ _ _ _ _ _ x U (@homOfLE (Opens _) _ U _ e)

open AlgebraicGeometry

/-- The restriction of a section along an inclusion of open sets.
For `x : F.obj (op V)`, we provide the notation `x |_ U`, where the proof `U ‚â§ V` is inferred by
the tactic `Top.presheaf.restrict_tac'` -/
abbrev restrictOpen {X : TopCat} {C : Type*} [Category C] [ConcreteCategory C] {F : X.Presheaf C}
    {V : Opens X} (x : F.obj (op V)) (U : Opens X)
    (e : U ‚â§ V := by restrict_tac) :
    F.obj (op U) :=
  x |_‚Çó U ‚ü™e‚ü´
set_option linter.uppercaseLean3 false in
#align Top.presheaf.restrict_open TopCat.Presheaf.restrictOpen

/-- restriction of a section to open subset -/
scoped[AlgebraicGeometry] infixl:80 " |_ " => TopCat.Presheaf.restrictOpen

-- Porting note: linter tells this lemma is no going to be picked up by the simplifier, hence
-- `@[simp]` is removed
theorem restrict_restrict {X : TopCat} {C : Type*} [Category C] [ConcreteCategory C]
    {F : X.Presheaf C} {U V W : Opens X} (e‚ÇÅ : U ‚â§ V) (e‚ÇÇ : V ‚â§ W) (x : F.obj (op W)) :
    x |_ V |_ U = x |_ U := by
  delta restrictOpen restrict
  rw [‚Üê comp_apply, ‚Üê Functor.map_comp]
  rfl
set_option linter.uppercaseLean3 false in
#align Top.presheaf.restrict_restrict TopCat.Presheaf.restrict_restrict

-- Porting note: linter tells this lemma is no going to be picked up by the simplifier, hence
-- `@[simp]` is removed
theorem map_restrict {X : TopCat} {C : Type*} [Category C] [ConcreteCategory C]
    {F G : X.Presheaf C} (e : F ‚ü∂ G) {U V : Opens X} (h : U ‚â§ V) (x : F.obj (op V)) :
    e.app _ (x |_ U) = e.app _ x |_ U := by
  delta restrictOpen restrict
  rw [‚Üê comp_apply, NatTrans.naturality, comp_apply]
set_option linter.uppercaseLean3 false in
#align Top.presheaf.map_restrict TopCat.Presheaf.map_restrict

open CategoryTheory.Limits

variable (C)

/-- The pushforward functor. -/
@[simps!]
def pushforward {X Y : TopCat.{w}} (f : X ‚ü∂ Y) : X.Presheaf C ‚•§ Y.Presheaf C :=
  (whiskeringLeft _ _ _).obj (Opens.map f).op

set_option quotPrecheck false in
/-- push forward of a presheaf-/
notation f:80 " _* " P:81 => (pushforward _ f).obj P

@[simp]
theorem pushforward_map_app' {X Y : TopCat.{w}} (f : X ‚ü∂ Y) {‚Ñ± ùí¢ : X.Presheaf C} (Œ± : ‚Ñ± ‚ü∂ ùí¢)
    {U : (Opens Y)·µí·µñ} : ((pushforward C f).map Œ±).app U = Œ±.app (op <| (Opens.map f).obj U.unop) :=
  rfl
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward_map_app' TopCat.Presheaf.pushforward_map_app'

lemma id_pushforward (X : TopCat.{w}) : pushforward C (ùüô X) = ùü≠ (X.Presheaf C) := rfl
set_option linter.uppercaseLean3 false in
#align Top.presheaf.id_pushforward TopCat.Presheaf.id_pushforward

variable {C}

namespace Pushforward

/-- The natural isomorphism between the pushforward of a presheaf along the identity continuous map
and the original presheaf. -/
def id {X : TopCat.{w}} (‚Ñ± : X.Presheaf C) : ùüô X _* ‚Ñ± ‚âÖ ‚Ñ± := Iso.refl _
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward.id TopCat.Presheaf.Pushforward.id

@[simp]
theorem id_hom_app {X : TopCat.{w}} (‚Ñ± : X.Presheaf C) (U) : (id ‚Ñ±).hom.app U = ùüô _ := rfl
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward.id_hom_app TopCat.Presheaf.Pushforward.id_hom_app

@[simp]
theorem id_inv_app {X : TopCat.{w}} (‚Ñ± : X.Presheaf C) (U) :
    (id ‚Ñ±).inv.app U = ùüô _ := rfl
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward.id_inv_app' TopCat.Presheaf.Pushforward.id_inv_app

theorem id_eq {X : TopCat.{w}} (‚Ñ± : X.Presheaf C) : ùüô X _* ‚Ñ± = ‚Ñ± := rfl
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward.id_eq TopCat.Presheaf.Pushforward.id_eq

/-- The natural isomorphism between
the pushforward of a presheaf along the composition of two continuous maps and
the corresponding pushforward of a pushforward. -/
def comp {X Y Z : TopCat.{w}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (‚Ñ± : X.Presheaf C) :
    (f ‚â´ g) _* ‚Ñ± ‚âÖ g _* (f _* ‚Ñ±) := Iso.refl _
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward.comp TopCat.Presheaf.Pushforward.comp

theorem comp_eq {X Y Z : TopCat.{w}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (‚Ñ± : X.Presheaf C) :
    (f ‚â´ g) _* ‚Ñ± = g _* (f _* ‚Ñ±) :=
  rfl
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward.comp_eq TopCat.Presheaf.Pushforward.comp_eq

@[simp]
theorem comp_hom_app {X Y Z : TopCat.{w}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (‚Ñ± : X.Presheaf C) (U) :
    (comp f g ‚Ñ±).hom.app U = ùüô _ := rfl
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward.comp_hom_app TopCat.Presheaf.Pushforward.comp_hom_app

@[simp]
theorem comp_inv_app {X Y Z : TopCat.{w}} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (‚Ñ± : X.Presheaf C) (U) :
    (comp f g ‚Ñ±).inv.app U = ùüô _ := rfl
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward.comp_inv_app TopCat.Presheaf.Pushforward.comp_inv_app

end Pushforward

/--
An equality of continuous maps induces a natural isomorphism between the pushforwards of a presheaf
along those maps.
-/
def pushforwardEq {X Y : TopCat.{w}} {f g : X ‚ü∂ Y} (h : f = g) (‚Ñ± : X.Presheaf C) :
    f _* ‚Ñ± ‚âÖ g _* ‚Ñ± :=
  isoWhiskerRight (NatIso.op (Opens.mapIso f g h).symm) ‚Ñ±
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward_eq TopCat.Presheaf.pushforwardEq

theorem pushforward_eq' {X Y : TopCat.{w}} {f g : X ‚ü∂ Y} (h : f = g) (‚Ñ± : X.Presheaf C) :
    f _* ‚Ñ± = g _* ‚Ñ± := by rw [h]
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward_eq' TopCat.Presheaf.pushforward_eq'

@[simp]
theorem pushforwardEq_hom_app {X Y : TopCat.{w}} {f g : X ‚ü∂ Y}
    (h : f = g) (‚Ñ± : X.Presheaf C) (U) :
    (pushforwardEq h ‚Ñ±).hom.app U = ‚Ñ±.map (eqToHom (by aesop_cat)) := by
  simp [pushforwardEq]
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward_eq_hom_app TopCat.Presheaf.pushforwardEq_hom_app

variable (C)

section Iso

/-- A homeomorphism of spaces gives an equivalence of categories of presheaves. -/
@[simps!]
def presheafEquivOfIso {X Y : TopCat} (H : X ‚âÖ Y) : X.Presheaf C ‚âå Y.Presheaf C :=
  Equivalence.congrLeft (Opens.mapMapIso H).symm.op
set_option linter.uppercaseLean3 false in
#align Top.presheaf.presheaf_equiv_of_iso TopCat.Presheaf.presheafEquivOfIso

variable {C}

/-- If `H : X ‚âÖ Y` is a homeomorphism,
then given an `H _* ‚Ñ± ‚ü∂ ùí¢`, we may obtain an `‚Ñ± ‚ü∂ H ‚Åª¬π _* ùí¢`.
-/
def toPushforwardOfIso {X Y : TopCat} (H : X ‚âÖ Y) {‚Ñ± : X.Presheaf C} {ùí¢ : Y.Presheaf C}
    (Œ± : H.hom _* ‚Ñ± ‚ü∂ ùí¢) : ‚Ñ± ‚ü∂ H.inv _* ùí¢ :=
  (presheafEquivOfIso _ H).toAdjunction.homEquiv ‚Ñ± ùí¢ Œ±
set_option linter.uppercaseLean3 false in
#align Top.presheaf.to_pushforward_of_iso TopCat.Presheaf.toPushforwardOfIso

@[simp]
theorem toPushforwardOfIso_app {X Y : TopCat} (H‚ÇÅ : X ‚âÖ Y) {‚Ñ± : X.Presheaf C} {ùí¢ : Y.Presheaf C}
    (H‚ÇÇ : H‚ÇÅ.hom _* ‚Ñ± ‚ü∂ ùí¢) (U : (Opens X)·µí·µñ) :
    (toPushforwardOfIso H‚ÇÅ H‚ÇÇ).app U =
      ‚Ñ±.map (eqToHom (by simp [Opens.map, Set.preimage_preimage])) ‚â´
        H‚ÇÇ.app (op ((Opens.map H‚ÇÅ.inv).obj (unop U))) := by
  delta toPushforwardOfIso
  simp [-Functor.map_comp, ‚Üê Functor.map_comp_assoc]
  rfl
set_option linter.uppercaseLean3 false in
#align Top.presheaf.to_pushforward_of_iso_app TopCat.Presheaf.toPushforwardOfIso_app

/-- If `H : X ‚âÖ Y` is a homeomorphism,
then given an `H _* ‚Ñ± ‚ü∂ ùí¢`, we may obtain an `‚Ñ± ‚ü∂ H ‚Åª¬π _* ùí¢`.
-/
def pushforwardToOfIso {X Y : TopCat} (H‚ÇÅ : X ‚âÖ Y) {‚Ñ± : Y.Presheaf C} {ùí¢ : X.Presheaf C}
    (H‚ÇÇ : ‚Ñ± ‚ü∂ H‚ÇÅ.hom _* ùí¢) : H‚ÇÅ.inv _* ‚Ñ± ‚ü∂ ùí¢ :=
  ((presheafEquivOfIso _ H‚ÇÅ.symm).toAdjunction.homEquiv ‚Ñ± ùí¢).symm H‚ÇÇ
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward_to_of_iso TopCat.Presheaf.pushforwardToOfIso

@[simp]
theorem pushforwardToOfIso_app {X Y : TopCat} (H‚ÇÅ : X ‚âÖ Y) {‚Ñ± : Y.Presheaf C} {ùí¢ : X.Presheaf C}
    (H‚ÇÇ : ‚Ñ± ‚ü∂ H‚ÇÅ.hom _* ùí¢) (U : (Opens X)·µí·µñ) :
    (pushforwardToOfIso H‚ÇÅ H‚ÇÇ).app U =
      H‚ÇÇ.app (op ((Opens.map H‚ÇÅ.inv).obj (unop U))) ‚â´
        ùí¢.map (eqToHom (by simp [Opens.map, Set.preimage_preimage])) := by
  simp [pushforwardToOfIso, Equivalence.toAdjunction]
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward_to_of_iso_app TopCat.Presheaf.pushforwardToOfIso_app

end Iso

variable [HasColimits C]

noncomputable section

/-- Pullback a presheaf on `Y` along a continuous map `f : X ‚ü∂ Y`, obtaining a presheaf
on `X`. -/
def pullback {X Y : TopCat.{v}} (f : X ‚ü∂ Y) : Y.Presheaf C ‚•§ X.Presheaf C :=
  (Opens.map f).op.lan
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pullback TopCat.Presheaf.pullback

/-- The pullback and pushforward along a continuous map are adjoint to each other. -/
def pushforwardPullbackAdjunction {X Y : TopCat.{v}} (f : X ‚ü∂ Y) :
    pullback C f ‚ä£ pushforward C f :=
  Functor.lanAdjunction _ _
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward_pullback_adjunction TopCat.Presheaf.pushforwardPullbackAdjunction

/-- Pulling back along a homeomorphism is the same as pushing forward along its inverse. -/
def pullbackHomIsoPushforwardInv {X Y : TopCat.{v}} (H : X ‚âÖ Y) :
    pullback C H.hom ‚âÖ pushforward C H.inv :=
  Adjunction.leftAdjointUniq (pushforwardPullbackAdjunction C H.hom)
    (presheafEquivOfIso C H.symm).toAdjunction
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pullback_hom_iso_pushforward_inv TopCat.Presheaf.pullbackHomIsoPushforwardInv

/-- Pulling back along the inverse of a homeomorphism is the same as pushing forward along it. -/
def pullbackInvIsoPushforwardHom {X Y : TopCat.{v}} (H : X ‚âÖ Y) :
    pullback C H.inv ‚âÖ pushforward C H.hom :=
  Adjunction.leftAdjointUniq (pushforwardPullbackAdjunction C H.inv)
    (presheafEquivOfIso C H).toAdjunction
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pullback_inv_iso_pushforward_hom TopCat.Presheaf.pullbackInvIsoPushforwardHom

variable {C}

/-- If `f '' U` is open, then `f‚Åª¬π‚Ñ± U ‚âÖ ‚Ñ± (f '' U)`.  -/
def pullbackObjObjOfImageOpen {X Y : TopCat.{v}} (f : X ‚ü∂ Y) (‚Ñ± : Y.Presheaf C) (U : Opens X)
    (H : IsOpen (f '' SetLike.coe U)) : ((pullback C f).obj ‚Ñ±).obj (op U) ‚âÖ ‚Ñ±.obj (op ‚ü®_, H‚ü©) := by
  let x : CostructuredArrow (Opens.map f).op (op U) := CostructuredArrow.mk
    (@homOfLE _ _ _ ((Opens.map f).obj ‚ü®_, H‚ü©) (Set.image_preimage.le_u_l _)).op
  have hx : IsTerminal x :=
    { lift := fun s ‚Ü¶ by
        fapply CostructuredArrow.homMk
        ¬∑ change op (unop _) ‚ü∂ op (‚ü®_, H‚ü© : Opens _)
          refine (homOfLE ?_).op
          apply (Set.image_subset f s.pt.hom.unop.le).trans
          exact Set.image_preimage.l_u_le (SetLike.coe s.pt.left.unop)
        ¬∑ simp [autoParam, eq_iff_true_of_subsingleton]
      -- Porting note: add `fac`, `uniq` manually
      fac := fun _ _ => by ext; simp [eq_iff_true_of_subsingleton]
      uniq := fun _ _ _ => by ext; simp [eq_iff_true_of_subsingleton] }
  exact IsColimit.coconePointUniqueUpToIso
    ((Opens.map f).op.isPointwiseLeftKanExtensionLanUnit ‚Ñ± (op U))
    (colimitOfDiagramTerminal hx _)
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pullback_obj_obj_of_image_open TopCat.Presheaf.pullbackObjObjOfImageOpen

end

end Presheaf

end TopCat
