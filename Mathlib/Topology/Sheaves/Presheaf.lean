/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Mario Carneiro, Reid Barton, Andrew Yang
-/
import Mathlib.CategoryTheory.Limits.KanExtension
import Mathlib.Topology.Category.TopCat.Opens
import Mathlib.CategoryTheory.Adjunction.Opposites
import Mathlib.Topology.Sheaves.Init

#align_import topology.sheaves.presheaf from "leanprover-community/mathlib"@"5dc6092d09e5e489106865241986f7f2ad28d4c8"

/-!
# Presheaves on a topological space

We define `TopCat.Presheaf C X` simply as `(TopologicalSpace.Opens X)áµ’áµ– â¥¤ C`,
and inherit the category structure with natural transformations as morphisms.

We define
* `TopCat.Presheaf.pushforwardObj {X Y : Top.{w}} (f : X âŸ¶ Y) (â„± : X.Presheaf C) : Y.Presheaf C`
with notation `f _* â„±`
and for `â„± : X.Presheaf C` provide the natural isomorphisms
* `TopCat.Presheaf.Pushforward.id : (ğŸ™ X) _* â„± â‰… â„±`
* `TopCat.Presheaf.Pushforward.comp : (f â‰« g) _* â„± â‰… g _* (f _* â„±)`
along with their `@[simp]` lemmas.

We also define the functors `pushforward` and `pullback` between the categories
`X.Presheaf C` and `Y.Presheaf C`, and provide their adjunction at
`TopCat.Presheaf.pushforwardPullbackAdjunction`.
-/

set_option autoImplicit true


universe w v u

open CategoryTheory TopologicalSpace Opposite

variable (C : Type u) [Category.{v} C]

namespace TopCat

/-- The category of `C`-valued presheaves on a (bundled) topological space `X`. -/
-- porting note: was @[nolint has_nonempty_instance]
def Presheaf (X : TopCat.{w}) : Type max u v w :=
  (Opens X)áµ’áµ– â¥¤ C
set_option linter.uppercaseLean3 false in
#align Top.presheaf TopCat.Presheaf

instance (X : TopCat.{w}) : Category (Presheaf.{w, v, u} C X) :=
  inferInstanceAs (Category ((Opens X)áµ’áµ– â¥¤ C : Type max u v w))

variable {C}

namespace Presheaf

-- Porting note: added an `ext` lemma,
-- since `NatTrans.ext` can not see through the definition of `Presheaf`.
-- See https://github.com/leanprover-community/mathlib4/issues/5229
@[ext]
lemma ext {P Q : Presheaf C X} {f g : P âŸ¶ Q} (w : âˆ€ U : Opens X, f.app (op U) = g.app (op U)) :
    f = g := by
  apply NatTrans.ext
  -- âŠ¢ f.app = g.app
  ext U
  -- âŠ¢ NatTrans.app f U = NatTrans.app g U
  induction U with | _ U => ?_
  -- âŠ¢ NatTrans.app f (op U) = NatTrans.app g (op U)
  -- âŠ¢ NatTrans.app f U = NatTrans.app g U
  apply w
  -- ğŸ‰ no goals

attribute [local instance] CategoryTheory.ConcreteCategory.hasCoeToSort
  CategoryTheory.ConcreteCategory.funLike

/-- attribute `sheaf_restrict` to mark lemmas related to restricting sheaves -/
macro "sheaf_restrict" : attr =>
  `(attr|aesop safe apply (rule_sets [$(Lean.mkIdent `Restrict):ident]))

attribute [sheaf_restrict] bot_le le_top le_refl inf_le_left inf_le_right
  le_sup_left le_sup_right

/-- `restrict_tac` solves relations among subsets (copied from `aesop cat`) -/
macro (name := restrict_tac) "restrict_tac" c:Aesop.tactic_clause* : tactic =>
`(tactic|
  aesop $c* (options := { introsTransparency? := some .default, terminal := true })
  (rule_sets [$(Lean.mkIdent `Restrict):ident]))

/-- `restrict_tac?` passes along `Try this` from `aesop` -/
macro (name := restrict_tac?) "restrict_tac?" c:Aesop.tactic_clause* : tactic =>
`(tactic|
  aesop? $c* (options := { introsTransparency? := some .default, terminal := true })
  (rule_sets [$(Lean.mkIdent `Restrict):ident]))

example {X : TopCat} {v w x y z : Opens X} (hâ‚€ : v â‰¤ x) (hâ‚ : x â‰¤ z âŠ“ w) (hâ‚‚ : x â‰¤ y âŠ“ z) : v â‰¤ y :=
  by restrict_tac
     -- ğŸ‰ no goals


/-- The restriction of a section along an inclusion of open sets.
For `x : F.obj (op V)`, we provide the notation `x |_â‚• i` (`h` stands for `hom`) for `i : U âŸ¶ V`,
and the notation `x |_â‚— U âŸªiâŸ«` (`l` stands for `le`) for `i : U â‰¤ V`.
-/
def restrict {X : TopCat} {C : Type*} [Category C] [ConcreteCategory C] {F : X.Presheaf C}
    {V : Opens X} (x : F.obj (op V)) {U : Opens X} (h : U âŸ¶ V) : F.obj (op U) :=
  F.map h.op x
set_option linter.uppercaseLean3 false in
#align Top.presheaf.restrict TopCat.Presheaf.restrict

/-- restriction of a section along an inclusion -/
scoped[AlgebraicGeometry] infixl:80 " |_â‚• " => TopCat.Presheaf.restrict
/-- restriction of a section along a subset relation -/
scoped[AlgebraicGeometry] notation:80 x " |_â‚— " U " âŸª" e "âŸ« " =>
  @TopCat.Presheaf.restrict _ _ _ _ _ _ x U (@homOfLE (Opens _) _ U _ e)

open AlgebraicGeometry

/-- The restriction of a section along an inclusion of open sets.
For `x : F.obj (op V)`, we provide the notation `x |_ U`, where the proof `U â‰¤ V` is inferred by
the tactic `Top.presheaf.restrict_tac'` -/
abbrev restrictOpen {X : TopCat} {C : Type*} [Category C] [ConcreteCategory C] {F : X.Presheaf C}
    {V : Opens X} (x : F.obj (op V)) (U : Opens X)
    (e : U â‰¤ V := by restrict_tac) :
    F.obj (op U) :=
  x |_â‚— U âŸªeâŸ«
set_option linter.uppercaseLean3 false in
#align Top.presheaf.restrict_open TopCat.Presheaf.restrictOpen

/-- restriction of a section to open subset -/
scoped[AlgebraicGeometry] infixl:80 " |_ " => TopCat.Presheaf.restrictOpen

-- Porting note : linter tells this lemma is no going to be picked up by the simplifier, hence
-- `@[simp]` is removed
theorem restrict_restrict {X : TopCat} {C : Type*} [Category C] [ConcreteCategory C]
    {F : X.Presheaf C} {U V W : Opens X} (eâ‚ : U â‰¤ V) (eâ‚‚ : V â‰¤ W) (x : F.obj (op W)) :
    x |_ V |_ U = x |_ U := by
  delta restrictOpen restrict
  -- âŠ¢ â†‘(F.map (homOfLE (_ : âˆ€ â¦ƒa : â†‘Xâ¦„, a âˆˆ â†‘U â†’ a âˆˆ â†‘V)).op) (â†‘(F.map (homOfLE (_ â€¦
  rw [â† comp_apply, â† Functor.map_comp]
  -- âŠ¢ â†‘(F.map ((homOfLE (_ : âˆ€ â¦ƒa : â†‘Xâ¦„, a âˆˆ â†‘V â†’ a âˆˆ â†‘W)).op â‰« (homOfLE (_ : âˆ€ â¦ƒa â€¦
  rfl
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align Top.presheaf.restrict_restrict TopCat.Presheaf.restrict_restrict

-- Porting note : linter tells this lemma is no going to be picked up by the simplifier, hence
-- `@[simp]` is removed
theorem map_restrict {X : TopCat} {C : Type*} [Category C] [ConcreteCategory C]
    {F G : X.Presheaf C} (e : F âŸ¶ G) {U V : Opens X} (h : U â‰¤ V) (x : F.obj (op V)) :
    e.app _ (x |_ U) = e.app _ x |_ U := by
  delta restrictOpen restrict
  -- âŠ¢ â†‘(NatTrans.app e (op U)) (â†‘(F.map (homOfLE (_ : âˆ€ â¦ƒa : â†‘Xâ¦„, a âˆˆ â†‘U â†’ a âˆˆ â†‘V) â€¦
  rw [â† comp_apply, NatTrans.naturality, comp_apply]
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align Top.presheaf.map_restrict TopCat.Presheaf.map_restrict

/-- Pushforward a presheaf on `X` along a continuous map `f : X âŸ¶ Y`, obtaining a presheaf
on `Y`. -/
def pushforwardObj {X Y : TopCat.{w}} (f : X âŸ¶ Y) (â„± : X.Presheaf C) : Y.Presheaf C :=
  (Opens.map f).op â‹™ â„±
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward_obj TopCat.Presheaf.pushforwardObj

/-- push forward of a presheaf-/
infixl:80 " _* " => pushforwardObj

@[simp]
theorem pushforwardObj_obj {X Y : TopCat.{w}} (f : X âŸ¶ Y) (â„± : X.Presheaf C) (U : (Opens Y)áµ’áµ–) :
    (f _* â„±).obj U = â„±.obj ((Opens.map f).op.obj U) :=
  rfl
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward_obj_obj TopCat.Presheaf.pushforwardObj_obj

@[simp]
theorem pushforwardObj_map {X Y : TopCat.{w}} (f : X âŸ¶ Y) (â„± : X.Presheaf C) {U V : (Opens Y)áµ’áµ–}
    (i : U âŸ¶ V) : (f _* â„±).map i = â„±.map ((Opens.map f).op.map i) :=
  rfl
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward_obj_map TopCat.Presheaf.pushforwardObj_map

/--
An equality of continuous maps induces a natural isomorphism between the pushforwards of a presheaf
along those maps.
-/
def pushforwardEq {X Y : TopCat.{w}} {f g : X âŸ¶ Y} (h : f = g) (â„± : X.Presheaf C) :
    f _* â„± â‰… g _* â„± :=
  isoWhiskerRight (NatIso.op (Opens.mapIso f g h).symm) â„±
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward_eq TopCat.Presheaf.pushforwardEq

theorem pushforward_eq' {X Y : TopCat.{w}} {f g : X âŸ¶ Y} (h : f = g) (â„± : X.Presheaf C) :
    f _* â„± = g _* â„± := by rw [h]
                          -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward_eq' TopCat.Presheaf.pushforward_eq'

@[simp]
theorem pushforwardEq_hom_app {X Y : TopCat.{w}} {f g : X âŸ¶ Y}
  (h : f = g) (â„± : X.Presheaf C) (U) :
    (pushforwardEq h â„±).hom.app U =
      â„±.map (by dsimp [Functor.op]; apply Quiver.Hom.op; apply eqToHom; rw [h]) :=
                -- âŠ¢ op ((Opens.map f).obj U.unop) âŸ¶ op ((Opens.map g).obj U.unop)
                                    -- âŠ¢ (Opens.map g).obj U.unop âŸ¶ (Opens.map f).obj U.unop
                                                         -- âŠ¢ (Opens.map g).obj U.unop = (Opens.map f).obj U.unop
                                                                        -- ğŸ‰ no goals
  by simp [pushforwardEq]
     -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward_eq_hom_app TopCat.Presheaf.pushforwardEq_hom_app

theorem pushforward_eq'_hom_app {X Y : TopCat.{w}} {f g : X âŸ¶ Y} (h : f = g) (â„± : X.Presheaf C)
    (U) : NatTrans.app (eqToHom (pushforward_eq' h â„±)) U = â„±.map (eqToHom (by rw [h])) := by
                                                                              -- ğŸ‰ no goals
  rw [eqToHom_app, eqToHom_map]
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward_eq'_hom_app TopCat.Presheaf.pushforward_eq'_hom_app

-- Porting note : This lemma is promoted to a higher priority to short circuit the simplifier
@[simp (high)]
theorem pushforwardEq_rfl {X Y : TopCat.{w}} (f : X âŸ¶ Y) (â„± : X.Presheaf C) (U) :
    (pushforwardEq (rfl : f = f) â„±).hom.app (op U) = ğŸ™ _ := by
  dsimp [pushforwardEq]
  -- âŠ¢ â„±.map (ğŸ™ (op ((Opens.map f).obj U))) = ğŸ™ (â„±.obj (op ((Opens.map f).obj U)))
  simp
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward_eq_rfl TopCat.Presheaf.pushforwardEq_rfl

theorem pushforwardEq_eq {X Y : TopCat.{w}} {f g : X âŸ¶ Y} (hâ‚ hâ‚‚ : f = g) (â„± : X.Presheaf C) :
    â„±.pushforwardEq hâ‚ = â„±.pushforwardEq hâ‚‚ :=
  rfl
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward_eq_eq TopCat.Presheaf.pushforwardEq_eq

namespace Pushforward

variable {X : TopCat.{w}} (â„± : X.Presheaf C)

/-- The natural isomorphism between the pushforward of a presheaf along the identity continuous map
and the original presheaf. -/
def id : ğŸ™ X _* â„± â‰… â„± :=
  isoWhiskerRight (NatIso.op (Opens.mapId X).symm) â„± â‰ªâ‰« Functor.leftUnitor _
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward.id TopCat.Presheaf.Pushforward.id

theorem id_eq : ğŸ™ X _* â„± = â„± := by
  unfold pushforwardObj
  -- âŠ¢ (Opens.map (ğŸ™ X)).op â‹™ â„± = â„±
  rw [Opens.map_id_eq]
  -- âŠ¢ (ğŸ­ (Opens â†‘X)).op â‹™ â„± = â„±
  erw [Functor.id_comp]
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward.id_eq TopCat.Presheaf.Pushforward.id_eq

-- Porting note : This lemma is promoted to a higher priority to short circuit the simplifier
@[simp (high)]
theorem id_hom_app' (U) (p) : (id â„±).hom.app (op âŸ¨U, pâŸ©) = â„±.map (ğŸ™ (op âŸ¨U, pâŸ©)) := by
  dsimp [id]
  -- âŠ¢ NatTrans.app (whiskerRight (NatTrans.op (Opens.mapId X).inv) â„± â‰« (Functor.le â€¦
  simp [CategoryStruct.comp]
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward.id_hom_app' TopCat.Presheaf.Pushforward.id_hom_app'

-- Porting note:
-- the proof below could be `by aesop_cat` if
-- https://github.com/JLimperg/aesop/issues/59
-- can be resolved, and we add:
attribute [local aesop safe cases (rule_sets [CategoryTheory])] Opposite
attribute [local aesop safe cases (rule_sets [CategoryTheory])] Opens

@[simp]
theorem id_hom_app (U) : (id â„±).hom.app U = â„±.map (eqToHom (Opens.op_map_id_obj U)) := by
  -- was `tidy`, see porting note above.
  induction U
  -- âŠ¢ NatTrans.app (id â„±).hom (op Xâœ) = â„±.map (eqToHom (_ : (Opens.map (ğŸ™ X)).op.o â€¦
  apply id_hom_app'
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward.id_hom_app TopCat.Presheaf.Pushforward.id_hom_app

@[simp]
theorem id_inv_app' (U) (p) : (id â„±).inv.app (op âŸ¨U, pâŸ©) = â„±.map (ğŸ™ (op âŸ¨U, pâŸ©)) := by
  dsimp [id]
  -- âŠ¢ NatTrans.app ((Functor.leftUnitor â„±).inv â‰« whiskerRight (NatTrans.op (Opens. â€¦
  simp [CategoryStruct.comp]
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward.id_inv_app' TopCat.Presheaf.Pushforward.id_inv_app'

/-- The natural isomorphism between
the pushforward of a presheaf along the composition of two continuous maps and
the corresponding pushforward of a pushforward. -/
def comp {Y Z : TopCat.{w}} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) : (f â‰« g) _* â„± â‰… g _* (f _* â„±) :=
  isoWhiskerRight (NatIso.op (Opens.mapComp f g).symm) â„±
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward.comp TopCat.Presheaf.Pushforward.comp

theorem comp_eq {Y Z : TopCat.{w}} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) : (f â‰« g) _* â„± = g _* (f _* â„±) :=
  rfl
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward.comp_eq TopCat.Presheaf.Pushforward.comp_eq

@[simp]
theorem comp_hom_app {Y Z : TopCat.{w}} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (U) :
    (comp â„± f g).hom.app U = ğŸ™ _ := by simp [comp]
                                       -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward.comp_hom_app TopCat.Presheaf.Pushforward.comp_hom_app

@[simp]
theorem comp_inv_app {Y Z : TopCat.{w}} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (U) :
    (comp â„± f g).inv.app U = ğŸ™ _ := by simp [comp]
                                       -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward.comp_inv_app TopCat.Presheaf.Pushforward.comp_inv_app

end Pushforward

/-- A morphism of presheaves gives rise to a morphisms of the pushforwards of those presheaves.
-/
@[simps]
def pushforwardMap {X Y : TopCat.{w}} (f : X âŸ¶ Y) {â„± ğ’¢ : X.Presheaf C} (Î± : â„± âŸ¶ ğ’¢) :
    f _* â„± âŸ¶ f _* ğ’¢ where
  app U := Î±.app _
  naturality _ _ i := by erw [Î±.naturality]; rfl
                         -- âŠ¢ NatTrans.app Î± ((Opens.map f).op.obj xâœÂ¹) â‰« ğ’¢.map ((Opens.map f).op.map i) = â€¦
                                             -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward_map TopCat.Presheaf.pushforwardMap

open CategoryTheory.Limits

noncomputable section Pullback

variable [HasColimits C]

/-- Pullback a presheaf on `Y` along a continuous map `f : X âŸ¶ Y`, obtaining a presheaf on `X`.

This is defined in terms of left Kan extensions, which is just a fancy way of saying
"take the colimits over the open sets whose preimage contains U".
-/
@[simps!]
def pullbackObj {X Y : TopCat.{v}} (f : X âŸ¶ Y) (â„± : Y.Presheaf C) : X.Presheaf C :=
  (lan (Opens.map f).op).obj â„±
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pullback_obj TopCat.Presheaf.pullbackObj

/-- Pulling back along continuous maps is functorial. -/
def pullbackMap {X Y : TopCat.{v}} (f : X âŸ¶ Y) {â„± ğ’¢ : Y.Presheaf C} (Î± : â„± âŸ¶ ğ’¢) :
    pullbackObj f â„± âŸ¶ pullbackObj f ğ’¢ :=
  (lan (Opens.map f).op).map Î±
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pullback_map TopCat.Presheaf.pullbackMap

/-- If `f '' U` is open, then `fâ»Â¹â„± U â‰… â„± (f '' U)`.  -/
@[simps!]
def pullbackObjObjOfImageOpen {X Y : TopCat.{v}} (f : X âŸ¶ Y) (â„± : Y.Presheaf C) (U : Opens X)
    (H : IsOpen (f '' SetLike.coe U)) : (pullbackObj f â„±).obj (op U) â‰… â„±.obj (op âŸ¨_, HâŸ©) := by
  let x : CostructuredArrow (Opens.map f).op (op U) := CostructuredArrow.mk
    (@homOfLE _ _ _ ((Opens.map f).obj âŸ¨_, HâŸ©) (Set.image_preimage.le_u_l _)).op
  have hx : IsTerminal x :=
    { lift := fun s â†¦ by
        fapply CostructuredArrow.homMk
        change op (unop _) âŸ¶ op (âŸ¨_, HâŸ© : Opens _)
        Â· refine' (homOfLE _).op
          apply (Set.image_subset f s.pt.hom.unop.le).trans
          exact Set.image_preimage.l_u_le (SetLike.coe s.pt.left.unop)
        Â· simp
      -- porting note : add `fac`, `uniq` manually
      fac := fun _ _ => by ext; simp
      uniq := fun _ _ _ => by ext; simp }
  exact IsColimit.coconePointUniqueUpToIso (colimit.isColimit _) (colimitOfDiagramTerminal hx _)
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pullback_obj_obj_of_image_open TopCat.Presheaf.pullbackObjObjOfImageOpen

namespace Pullback

variable {X Y : TopCat.{v}} (â„± : Y.Presheaf C)

/-- The pullback along the identity is isomorphic to the original presheaf. -/
def id : pullbackObj (ğŸ™ _) â„± â‰… â„± :=
  NatIso.ofComponents
    (fun U =>
      pullbackObjObjOfImageOpen (ğŸ™ _) â„± (unop U) (by simpa using U.unop.2) â‰ªâ‰«
                                                     -- ğŸ‰ no goals
        â„±.mapIso (eqToIso (by simp)))
                              -- ğŸ‰ no goals
    fun {U V} i => by
      simp only [pullbackObj_obj]
      -- âŠ¢ (pullbackObj (ğŸ™ Y) â„±).map i â‰« (pullbackObjObjOfImageOpen (ğŸ™ Y) â„± V.unop (_ : â€¦
      ext
      -- âŠ¢ colimit.Î¹ (Lan.diagram (Opens.map (ğŸ™ Y)).op â„± U) jâœ â‰« (pullbackObj (ğŸ™ Y) â„±). â€¦
      simp only [Functor.comp_obj, CostructuredArrow.proj_obj, pullbackObj_map,
        Iso.trans_hom, Functor.mapIso_hom, eqToIso.hom, Category.assoc]
      erw [colimit.pre_desc_assoc, colimit.Î¹_desc_assoc, colimit.Î¹_desc_assoc]
      -- âŠ¢ NatTrans.app
      dsimp
      -- âŠ¢ â„±.map (IsTerminal.from (IsLimit.mk fun s => CostructuredArrow.homMk (homOfLE â€¦
      simp only [â† â„±.map_comp]
      -- âŠ¢ â„±.map ((IsTerminal.from (IsLimit.mk fun s => CostructuredArrow.homMk (homOfL â€¦
      -- Porting note : `congr` does not work, but `congr 1` does
      congr 1
      -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pullback.id TopCat.Presheaf.Pullback.id

theorem id_inv_app (U : Opens Y) :
    (id â„±).inv.app (op U) =
      colimit.Î¹ (Lan.diagram (Opens.map (ğŸ™ Y)).op â„± (op U))
        (@CostructuredArrow.mk _ _ _ _ _ (op U) _ (eqToHom (by simp))) := by
                                                               -- ğŸ‰ no goals
  rw [â† Category.id_comp ((id â„±).inv.app (op U)), â† NatIso.app_inv, Iso.comp_inv_eq]
  -- âŠ¢ ğŸ™ (â„±.obj (op U)) = colimit.Î¹ (Lan.diagram (Opens.map (ğŸ™ Y)).op â„± (op U)) (Co â€¦
  dsimp [id]
  -- âŠ¢ ğŸ™ (â„±.obj (op U)) = colimit.Î¹ (Lan.diagram (Opens.map (ğŸ™ Y)).op â„± (op U)) (Co â€¦
  erw [colimit.Î¹_desc_assoc]
  -- âŠ¢ ğŸ™ (â„±.obj (op U)) = NatTrans.app (coconeOfDiagramTerminal (IsLimit.mk fun s = â€¦
  dsimp
  -- âŠ¢ ğŸ™ (â„±.obj (op U)) = â„±.map (IsTerminal.from (IsLimit.mk fun s => CostructuredA â€¦
  rw [â† â„±.map_comp, â† â„±.map_id]; rfl
  -- âŠ¢ â„±.map (ğŸ™ (op U)) = â„±.map ((IsTerminal.from (IsLimit.mk fun s => Costructured â€¦
                                 -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pullback.id_inv_app TopCat.Presheaf.Pullback.id_inv_app

end Pullback

end Pullback

variable (C)

/-- The pushforward functor.
-/
def pushforward {X Y : TopCat.{w}} (f : X âŸ¶ Y) : X.Presheaf C â¥¤ Y.Presheaf C where
  obj := pushforwardObj f
  map := @pushforwardMap _ _ X Y f
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward TopCat.Presheaf.pushforward

@[simp]
theorem pushforward_map_app' {X Y : TopCat.{w}} (f : X âŸ¶ Y) {â„± ğ’¢ : X.Presheaf C} (Î± : â„± âŸ¶ ğ’¢)
    {U : (Opens Y)áµ’áµ–} : ((pushforward C f).map Î±).app U = Î±.app (op <| (Opens.map f).obj U.unop) :=
  rfl
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward_map_app' TopCat.Presheaf.pushforward_map_app'

theorem id_pushforward {X : TopCat.{w}} : pushforward C (ğŸ™ X) = ğŸ­ (X.Presheaf C) := by
  apply CategoryTheory.Functor.ext
  -- âŠ¢ autoParam (âˆ€ (X_1 Y : Presheaf C X) (f : X_1 âŸ¶ Y), (pushforward C (ğŸ™ X)).map â€¦
  Â· intros a b f
    -- âŠ¢ (pushforward C (ğŸ™ X)).map f = eqToHom (_ : ?F.obj a = ?G.obj a) â‰« (ğŸ­ (Preshe â€¦
    ext U
    -- âŠ¢ NatTrans.app ((pushforward C (ğŸ™ X)).map f) (op U) = NatTrans.app (eqToHom (_ â€¦
    Â· erw [NatTrans.congr f (Opens.op_map_id_obj (op U))]
      -- âŠ¢ a.map (eqToHom (_ : (Opens.map (ğŸ™ X)).op.obj (op U) = op U)) â‰« NatTrans.app  â€¦
      simp only [Functor.op_obj, eqToHom_refl, CategoryTheory.Functor.map_id,
        Category.comp_id, Category.id_comp, Functor.id_obj, Functor.id_map]
      apply Pushforward.id_eq
      -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align Top.presheaf.id_pushforward TopCat.Presheaf.id_pushforward

section Iso

/-- A homeomorphism of spaces gives an equivalence of categories of presheaves. -/
@[simps!]
def presheafEquivOfIso {X Y : TopCat} (H : X â‰… Y) : X.Presheaf C â‰Œ Y.Presheaf C :=
  Equivalence.congrLeft (Opens.mapMapIso H).symm.op
set_option linter.uppercaseLean3 false in
#align Top.presheaf.presheaf_equiv_of_iso TopCat.Presheaf.presheafEquivOfIso

variable {C}

/-- If `H : X â‰… Y` is a homeomorphism,
then given an `H _* â„± âŸ¶ ğ’¢`, we may obtain an `â„± âŸ¶ H â»Â¹ _* ğ’¢`.
-/
def toPushforwardOfIso {X Y : TopCat} (H : X â‰… Y) {â„± : X.Presheaf C} {ğ’¢ : Y.Presheaf C}
    (Î± : H.hom _* â„± âŸ¶ ğ’¢) : â„± âŸ¶ H.inv _* ğ’¢ :=
  (presheafEquivOfIso _ H).toAdjunction.homEquiv â„± ğ’¢ Î±
set_option linter.uppercaseLean3 false in
#align Top.presheaf.to_pushforward_of_iso TopCat.Presheaf.toPushforwardOfIso

@[simp]
theorem toPushforwardOfIso_app {X Y : TopCat} (Hâ‚ : X â‰… Y) {â„± : X.Presheaf C} {ğ’¢ : Y.Presheaf C}
    (Hâ‚‚ : Hâ‚.hom _* â„± âŸ¶ ğ’¢) (U : (Opens X)áµ’áµ–) :
    (toPushforwardOfIso Hâ‚ Hâ‚‚).app U =
      â„±.map (eqToHom (by simp [Opens.map, Set.preimage_preimage])) â‰«
                         -- ğŸ‰ no goals
        Hâ‚‚.app (op ((Opens.map Hâ‚.inv).obj (unop U))) := by
  delta toPushforwardOfIso
  -- âŠ¢ NatTrans.app (â†‘(Adjunction.homEquiv (Equivalence.toAdjunction (presheafEquiv â€¦
  -- Porting note : originally is a single invocation of `simp`
  simp only [pushforwardObj_obj, Functor.op_obj, Equivalence.toAdjunction, Adjunction.homEquiv_unit,
    Functor.id_obj, Functor.comp_obj, Adjunction.mkOfUnitCounit_unit, unop_op, eqToHom_map]
  rw [NatTrans.comp_app, presheafEquivOfIso_inverse_map_app, Equivalence.Equivalence_mk'_unit]
  -- âŠ¢ NatTrans.app (NatTrans.app (presheafEquivOfIso C Hâ‚).unitIso.hom â„±) U â‰« NatT â€¦
  congr 1
  -- âŠ¢ NatTrans.app (NatTrans.app (presheafEquivOfIso C Hâ‚).unitIso.hom â„±) U = eqTo â€¦
  simp only [Equivalence.unit, Equivalence.op, CategoryTheory.Equivalence.symm, Opens.mapMapIso,
    Functor.id_obj, Functor.comp_obj, Iso.symm_hom, NatIso.op_inv, Iso.symm_inv, NatTrans.op_app,
    NatIso.ofComponents_hom_app, eqToIso.hom, eqToHom_op, Equivalence.Equivalence_mk'_unitInv,
    Equivalence.Equivalence_mk'_counitInv, NatIso.op_hom, unop_op, op_unop, eqToIso.inv,
    NatIso.ofComponents_inv_app, eqToHom_unop, â†â„±.map_comp, eqToHom_trans, eqToHom_map,
    presheafEquivOfIso_unitIso_hom_app_app]
set_option linter.uppercaseLean3 false in
#align Top.presheaf.to_pushforward_of_iso_app TopCat.Presheaf.toPushforwardOfIso_app

/-- If `H : X â‰… Y` is a homeomorphism,
then given an `H _* â„± âŸ¶ ğ’¢`, we may obtain an `â„± âŸ¶ H â»Â¹ _* ğ’¢`.
-/
def pushforwardToOfIso {X Y : TopCat} (Hâ‚ : X â‰… Y) {â„± : Y.Presheaf C} {ğ’¢ : X.Presheaf C}
    (Hâ‚‚ : â„± âŸ¶ Hâ‚.hom _* ğ’¢) : Hâ‚.inv _* â„± âŸ¶ ğ’¢ :=
  ((presheafEquivOfIso _ Hâ‚.symm).toAdjunction.homEquiv â„± ğ’¢).symm Hâ‚‚
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward_to_of_iso TopCat.Presheaf.pushforwardToOfIso

@[simp]
theorem pushforwardToOfIso_app {X Y : TopCat} (Hâ‚ : X â‰… Y) {â„± : Y.Presheaf C} {ğ’¢ : X.Presheaf C}
    (Hâ‚‚ : â„± âŸ¶ Hâ‚.hom _* ğ’¢) (U : (Opens X)áµ’áµ–) :
    (pushforwardToOfIso Hâ‚ Hâ‚‚).app U =
      Hâ‚‚.app (op ((Opens.map Hâ‚.inv).obj (unop U))) â‰«
        ğ’¢.map (eqToHom (by simp [Opens.map, Set.preimage_preimage])) := by
                           -- ğŸ‰ no goals
  simp [pushforwardToOfIso, Equivalence.toAdjunction, CategoryStruct.comp]
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward_to_of_iso_app TopCat.Presheaf.pushforwardToOfIso_app

end Iso

variable [HasColimits C]

noncomputable section

/-- Pullback a presheaf on `Y` along a continuous map `f : X âŸ¶ Y`, obtaining a presheaf
on `X`. -/
@[simps! map_app]
def pullback {X Y : TopCat.{v}} (f : X âŸ¶ Y) : Y.Presheaf C â¥¤ X.Presheaf C :=
  lan (Opens.map f).op
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pullback TopCat.Presheaf.pullback

@[simp]
theorem pullbackObj_eq_pullbackObj {C} [Category C] [HasColimits C] {X Y : TopCat.{w}} (f : X âŸ¶ Y)
    (â„± : Y.Presheaf C) : (pullback C f).obj â„± = pullbackObj f â„± :=
  rfl
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pullback_obj_eq_pullback_obj TopCat.Presheaf.pullbackObj_eq_pullbackObj

/-- The pullback and pushforward along a continuous map are adjoint to each other. -/
@[simps! unit_app_app counit_app_app]
def pushforwardPullbackAdjunction {X Y : TopCat.{v}} (f : X âŸ¶ Y) :
  pullback C f âŠ£ pushforward C f :=
  Lan.adjunction _ _
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pushforward_pullback_adjunction TopCat.Presheaf.pushforwardPullbackAdjunction

/-- Pulling back along a homeomorphism is the same as pushing forward along its inverse. -/
def pullbackHomIsoPushforwardInv {X Y : TopCat.{v}} (H : X â‰… Y) :
    pullback C H.hom â‰… pushforward C H.inv :=
  Adjunction.leftAdjointUniq (pushforwardPullbackAdjunction C H.hom)
    (presheafEquivOfIso C H.symm).toAdjunction
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pullback_hom_iso_pushforward_inv TopCat.Presheaf.pullbackHomIsoPushforwardInv

/-- Pulling back along the inverse of a homeomorphism is the same as pushing forward along it. -/
def pullbackInvIsoPushforwardHom {X Y : TopCat.{v}} (H : X â‰… Y) :
    pullback C H.inv â‰… pushforward C H.hom :=
  Adjunction.leftAdjointUniq (pushforwardPullbackAdjunction C H.inv)
    (presheafEquivOfIso C H).toAdjunction
set_option linter.uppercaseLean3 false in
#align Top.presheaf.pullback_inv_iso_pushforward_hom TopCat.Presheaf.pullbackInvIsoPushforwardHom

end

end Presheaf

end TopCat
