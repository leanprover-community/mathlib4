/-
Copyright (c) 2023 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Yury Kudryashov
-/
import Mathlib.Topology.NhdsSet
import Mathlib.Topology.Inseparable

/-!
# Neighborhoods kernel of a set

In `Mathlib/Topology/Defs/Filter.lean`, `nhdsKer s` is defined to be the intersection of all
neighborhoods of `s`.
Note that this construction has no standard name in the literature.

In this file we prove basic properties of this operation.
-/

open Set Filter
open scoped Topology

variable {Î¹ : Sort*} {X : Type*} [TopologicalSpace X] {s t : Set X} {x y : X}

lemma nhdsKer_singleton_eq_ker_nhds (x : X) : nhdsKer {x} = (ğ“ x).ker := by simp [nhdsKer]

@[deprecated (since := "2025-07-09")]
alias exterior_singleton_eq_ker_nhds := nhdsKer_singleton_eq_ker_nhds

@[simp]
theorem mem_nhdsKer_singleton : x âˆˆ nhdsKer {y} â†” x â¤³ y := by
  rw [nhdsKer_singleton_eq_ker_nhds, ker_nhds_eq_specializes, mem_setOf]

@[deprecated (since := "2025-07-09")] alias mem_exterior_singleton := mem_nhdsKer_singleton

lemma nhdsKer_def (s : Set X) : nhdsKer s = â‹‚â‚€ {t : Set X | IsOpen t âˆ§ s âŠ† t} :=
  (hasBasis_nhdsSet _).ker.trans sInter_eq_biInter.symm

@[deprecated (since := "2025-07-09")] alias exterior_def := nhdsKer_def

lemma mem_nhdsKer : x âˆˆ nhdsKer s â†” âˆ€ U, IsOpen U â†’ s âŠ† U â†’ x âˆˆ U := by simp [nhdsKer_def]

@[deprecated (since := "2025-07-09")] alias mem_exterior := mem_nhdsKer

lemma subset_nhdsKer_iff : s âŠ† nhdsKer t â†” âˆ€ U, IsOpen U â†’ t âŠ† U â†’ s âŠ† U := by
  simp [nhdsKer_def]

@[deprecated (since := "2025-07-09")] alias subset_exterior_iff := subset_nhdsKer_iff

lemma subset_nhdsKer : s âŠ† nhdsKer s := subset_nhdsKer_iff.2 fun _ _ â†¦ id

@[deprecated (since := "2025-07-09")] alias subset_exterior := subset_nhdsKer

lemma nhdsKer_minimal (hâ‚ : s âŠ† t) (hâ‚‚ : IsOpen t) : nhdsKer s âŠ† t := by
  rw [nhdsKer_def]; exact sInter_subset_of_mem âŸ¨hâ‚‚, hâ‚âŸ©

@[deprecated (since := "2025-07-09")] alias exterior_minimal := nhdsKer_minimal

lemma IsOpen.nhdsKer_eq (h : IsOpen s) : nhdsKer s = s :=
  (nhdsKer_minimal Subset.rfl h).antisymm subset_nhdsKer

@[deprecated (since := "2025-07-09")] alias IsOpen.exterior_eq := IsOpen.nhdsKer_eq

lemma IsOpen.nhdsKer_subset (ht : IsOpen t) : nhdsKer s âŠ† t â†” s âŠ† t :=
  âŸ¨subset_nhdsKer.trans, fun h â†¦ nhdsKer_minimal h htâŸ©

@[deprecated (since := "2025-07-09")] alias IsOpen.exterior_subset := IsOpen.nhdsKer_subset

@[simp]
theorem nhdsKer_iUnion (s : Î¹ â†’ Set X) : nhdsKer (â‹ƒ i, s i) = â‹ƒ i, nhdsKer (s i) := by
  simp only [nhdsKer, nhdsSet_iUnion, ker_iSup]

@[deprecated (since := "2025-07-09")] alias exterior_iUnion := nhdsKer_iUnion

@[simp]
theorem nhdsKer_union (s t : Set X) : nhdsKer (s âˆª t) = nhdsKer s âˆª nhdsKer t := by
  simp only [nhdsKer, nhdsSet_union, ker_sup]

@[deprecated (since := "2025-07-09")] alias exterior_union := nhdsKer_union

@[simp]
theorem nhdsKer_sUnion (S : Set (Set X)) : nhdsKer (â‹ƒâ‚€ S) = â‹ƒ s âˆˆ S, nhdsKer s := by
  simp only [sUnion_eq_biUnion, nhdsKer_iUnion]

@[deprecated (since := "2025-07-09")] alias exterior_sUnion := nhdsKer_sUnion

theorem mem_nhdsKer_iff_specializes : x âˆˆ nhdsKer s â†” âˆƒ y âˆˆ s, x â¤³ y := calc
  x âˆˆ nhdsKer s â†” x âˆˆ nhdsKer (â‹ƒ y âˆˆ s, {y}) := by simp
  _ â†” âˆƒ y âˆˆ s, x â¤³ y := by
    simp only [nhdsKer_iUnion, mem_nhdsKer_singleton, mem_iUnionâ‚‚, exists_prop]

@[deprecated (since := "2025-07-09")]
alias mem_exterior_iff_specializes := mem_nhdsKer_iff_specializes

@[mono] lemma nhdsKer_mono : Monotone (nhdsKer : Set X â†’ Set X) :=
  fun _s _t h â†¦ ker_mono <| nhdsSet_mono h

@[deprecated (since := "2025-07-09")] alias exterior_mono := nhdsKer_mono

/-- This name was used to be used for the `Iff` version,
see `nhdsKer_subset_nhdsKer_iff_nhdsSet`.
-/
@[gcongr] lemma nhdsKer_subset_nhdsKer (h : s âŠ† t) : nhdsKer s âŠ† nhdsKer t := nhdsKer_mono h

@[deprecated (since := "2025-07-09")] alias exterior_subset_exterior := nhdsKer_subset_nhdsKer

@[simp] lemma nhdsKer_subset_nhdsKer_iff_nhdsSet : nhdsKer s âŠ† nhdsKer t â†” ğ“Ë¢ s â‰¤ ğ“Ë¢ t := by
  simp +contextual only [subset_nhdsKer_iff, (hasBasis_nhdsSet _).ge_iff,
    and_imp, IsOpen.mem_nhdsSet, IsOpen.nhdsKer_subset]

@[deprecated (since := "2025-07-09")]
alias exterior_subset_exterior_iff_nhdsSet := nhdsKer_subset_nhdsKer_iff_nhdsSet

theorem nhdsKer_eq_nhdsKer_iff_nhdsSet : nhdsKer s = nhdsKer t â†” ğ“Ë¢ s = ğ“Ë¢ t := by
  simp [le_antisymm_iff]

@[deprecated (since := "2025-07-09")]
alias exterior_eq_exterior_iff_nhdsSet := nhdsKer_eq_nhdsKer_iff_nhdsSet

lemma specializes_iff_nhdsKer_subset : x â¤³ y â†” nhdsKer {x} âŠ† nhdsKer {y} := by
  simp [Specializes]

@[deprecated (since := "2025-07-09")]
alias specializes_iff_exterior_subset := specializes_iff_nhdsKer_subset

theorem nhdsKer_iInter_subset {s : Î¹ â†’ Set X} : nhdsKer (â‹‚ i, s i) âŠ† â‹‚ i, nhdsKer (s i) :=
  nhdsKer_mono.map_iInf_le

@[deprecated (since := "2025-07-09")] alias exterior_iInter_subset := nhdsKer_iInter_subset

theorem nhdsKer_inter_subset {s t : Set X} : nhdsKer (s âˆ© t) âŠ† nhdsKer s âˆ© nhdsKer t :=
  nhdsKer_mono.map_inf_le _ _

@[deprecated (since := "2025-07-09")] alias exterior_inter_subset := nhdsKer_inter_subset

theorem nhdsKer_sInter_subset {s : Set (Set X)} : nhdsKer (â‹‚â‚€ s) âŠ† â‹‚ x âˆˆ s, nhdsKer x :=
  nhdsKer_mono.map_sInf_le

@[deprecated (since := "2025-07-09")] alias exterior_sInter_subset := nhdsKer_sInter_subset

@[simp] lemma nhdsKer_empty : nhdsKer (âˆ… : Set X) = âˆ… := isOpen_empty.nhdsKer_eq

@[deprecated (since := "2025-07-09")] alias exterior_empty := nhdsKer_empty

@[simp] lemma nhdsKer_univ : nhdsKer (univ : Set X) = univ := isOpen_univ.nhdsKer_eq

@[deprecated (since := "2025-07-09")] alias exterior_univ := nhdsKer_univ

@[simp] lemma nhdsKer_eq_empty : nhdsKer s = âˆ… â†” s = âˆ… :=
  âŸ¨eq_bot_mono subset_nhdsKer, by rintro rfl; exact nhdsKer_emptyâŸ©

@[deprecated (since := "2025-07-09")] alias exterior_eq_empty := nhdsKer_eq_empty

@[simp] lemma nhdsSet_nhdsKer (s : Set X) : ğ“Ë¢ (nhdsKer s) = ğ“Ë¢ s := by
  refine le_antisymm ((hasBasis_nhdsSet _).ge_iff.2 ?_) (nhdsSet_mono subset_nhdsKer)
  exact fun U âŸ¨hUo, hsUâŸ© â†¦ hUo.mem_nhdsSet.2 <| hUo.nhdsKer_subset.2 hsU

@[deprecated (since := "2025-07-09")] alias nhdsSet_exterior := nhdsSet_nhdsKer

@[simp] lemma nhdsKer_nhdsKer (s : Set X) : nhdsKer (nhdsKer s) = nhdsKer s := by
  simp only [nhdsKer_eq_nhdsKer_iff_nhdsSet, nhdsSet_nhdsKer]

@[deprecated (since := "2025-07-09")] alias exterior_exterior := nhdsKer_nhdsKer
