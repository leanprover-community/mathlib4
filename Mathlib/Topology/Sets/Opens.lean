/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro, Floris van Doorn
-/
import Mathlib.Order.Hom.CompleteLattice
import Mathlib.Topology.Bases
import Mathlib.Topology.Homeomorph
import Mathlib.Topology.ContinuousFunction.Basic
import Mathlib.Order.CompactlyGenerated.Basic
import Mathlib.Order.Copy

/-!
# Open sets

## Summary

We define the subtype of open sets in a topological space.

## Main Definitions

### Bundled open sets

- `TopologicalSpace.Opens Œ±` is the type of open subsets of a topological space `Œ±`.
- `TopologicalSpace.Opens.IsBasis` is a predicate saying that a set of `Opens`s form a topological
  basis.
- `TopologicalSpace.Opens.comap`: preimage of an open set under a continuous map as a `FrameHom`.
- `Homeomorph.opensCongr`: order-preserving equivalence between open sets in the domain and the
  codomain of a homeomorphism.

### Bundled open neighborhoods

- `TopologicalSpace.OpenNhdsOf x` is the type of open subsets of a topological space `Œ±` containing
  `x : Œ±`.
- `TopologicalSpace.OpenNhdsOf.comap f x U` is the preimage of open neighborhood `U` of `f x` under
  `f : C(Œ±, Œ≤)`.

## Main results

We define order structures on both `Opens Œ±` (`CompleteLattice`, `Frame`) and `OpenNhdsOf x`
(`OrderTop`, `DistribLattice`).

## TODO

- Rename `TopologicalSpace.Opens` to `Open`?
- Port the `auto_cases` tactic version (as a plugin if the ported `auto_cases` will allow plugins).
-/


open Filter Function Order Set

open Topology

variable {Œπ Œ± Œ≤ Œ≥ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [TopologicalSpace Œ≥]

namespace TopologicalSpace

variable (Œ±)

/-- The type of open subsets of a topological space. -/
structure Opens where
  /-- The underlying set of a bundled `TopologicalSpace.Opens` object. -/
  carrier : Set Œ±
  /-- The `TopologicalSpace.Opens.carrier _` is an open set. -/
  is_open' : IsOpen carrier

variable {Œ±}

namespace Opens

instance : SetLike (Opens Œ±) Œ± where
  coe := Opens.carrier
  coe_injective' := fun ‚ü®_, _‚ü© ‚ü®_, _‚ü© _ => by congr

instance : CanLift (Set Œ±) (Opens Œ±) (‚Üë) IsOpen :=
  ‚ü®fun s h => ‚ü®‚ü®s, h‚ü©, rfl‚ü©‚ü©

instance instSecondCountableOpens [SecondCountableTopology Œ±] (U : Opens Œ±) :
    SecondCountableTopology U := inferInstanceAs (SecondCountableTopology U.1)

theorem ¬´forall¬ª {p : Opens Œ± ‚Üí Prop} : (‚àÄ U, p U) ‚Üî ‚àÄ (U : Set Œ±) (hU : IsOpen U), p ‚ü®U, hU‚ü© :=
  ‚ü®fun h _ _ => h _, fun h _ => h _ _‚ü©

@[simp] theorem carrier_eq_coe (U : Opens Œ±) : U.1 = ‚ÜëU := rfl

/-- the coercion `Opens Œ± ‚Üí Set Œ±` applied to a pair is the same as taking the first component -/
@[simp]
theorem coe_mk {U : Set Œ±} {hU : IsOpen U} : ‚Üë(‚ü®U, hU‚ü© : Opens Œ±) = U :=
  rfl

@[simp]
theorem mem_mk {x : Œ±} {U : Set Œ±} {h : IsOpen U} : x ‚àà mk U h ‚Üî x ‚àà U := Iff.rfl

-- Porting note: removed @[simp] because LHS simplifies to `‚àÉ x, x ‚àà U`
protected theorem nonempty_coeSort {U : Opens Œ±} : Nonempty U ‚Üî (U : Set Œ±).Nonempty :=
  Set.nonempty_coe_sort

-- TODO: should this theorem be proved for a `SetLike`?
protected theorem nonempty_coe {U : Opens Œ±} : (U : Set Œ±).Nonempty ‚Üî ‚àÉ x, x ‚àà U :=
  Iff.rfl

@[ext] -- Porting note (#11215): TODO: replace with `‚àÄ x, x ‚àà U ‚Üî x ‚àà V`
theorem ext {U V : Opens Œ±} (h : (U : Set Œ±) = V) : U = V :=
  SetLike.coe_injective h

-- Porting note: removed @[simp], simp can prove it
theorem coe_inj {U V : Opens Œ±} : (U : Set Œ±) = V ‚Üî U = V :=
  SetLike.ext'_iff.symm

/-- A version of `Set.inclusion` not requiring definitional abuse -/
abbrev inclusion {U V : Opens Œ±} (h : U ‚â§ V) : U ‚Üí V := Set.inclusion h

protected theorem isOpen (U : Opens Œ±) : IsOpen (U : Set Œ±) :=
  U.is_open'

@[simp] theorem mk_coe (U : Opens Œ±) : mk (‚ÜëU) U.isOpen = U := rfl

/-- See Note [custom simps projection]. -/
def Simps.coe (U : Opens Œ±) : Set Œ± := U

initialize_simps_projections Opens (carrier ‚Üí coe)

/-- The interior of a set, as an element of `Opens`. -/
@[simps]
protected def interior (s : Set Œ±) : Opens Œ± :=
  ‚ü®interior s, isOpen_interior‚ü©

@[simp]
theorem mem_interior {s : Set Œ±} {x : Œ±} : x ‚àà Opens.interior s ‚Üî x ‚àà _root_.interior s := .rfl

theorem gc : GaloisConnection ((‚Üë) : Opens Œ± ‚Üí Set Œ±) Opens.interior := fun U _ =>
  ‚ü®fun h => interior_maximal h U.isOpen, fun h => le_trans h interior_subset‚ü©

/-- The galois coinsertion between sets and opens. -/
def gi : GaloisCoinsertion (‚Üë) (@Opens.interior Œ± _) where
  choice s hs := ‚ü®s, interior_eq_iff_isOpen.mp <| le_antisymm interior_subset hs‚ü©
  gc := gc
  u_l_le _ := interior_subset
  choice_eq _s hs := le_antisymm hs interior_subset

instance : CompleteLattice (Opens Œ±) :=
  CompleteLattice.copy (GaloisCoinsertion.liftCompleteLattice gi)
    -- le
    (fun U V => (U : Set Œ±) ‚äÜ V) rfl
    -- top
    ‚ü®univ, isOpen_univ‚ü© (ext interior_univ.symm)
    -- bot
    ‚ü®‚àÖ, isOpen_empty‚ü© rfl
    -- sup
    (fun U V => ‚ü®‚ÜëU ‚à™ ‚ÜëV, U.2.union V.2‚ü©) rfl
    -- inf
    (fun U V => ‚ü®‚ÜëU ‚à© ‚ÜëV, U.2.inter V.2‚ü©)
    (funext‚ÇÇ fun U V => ext (U.2.inter V.2).interior_eq.symm)
    -- sSup
    (fun S => ‚ü®‚ãÉ s ‚àà S, ‚Üës, isOpen_biUnion fun s _ => s.2‚ü©)
    (funext fun _ => ext sSup_image.symm)
    -- sInf
    _ rfl

@[simp]
theorem mk_inf_mk {U V : Set Œ±} {hU : IsOpen U} {hV : IsOpen V} :
    (‚ü®U, hU‚ü© ‚äì ‚ü®V, hV‚ü© : Opens Œ±) = ‚ü®U ‚äì V, IsOpen.inter hU hV‚ü© :=
  rfl

@[simp, norm_cast]
theorem coe_inf (s t : Opens Œ±) : (‚Üë(s ‚äì t) : Set Œ±) = ‚Üës ‚à© ‚Üët :=
  rfl

@[simp, norm_cast]
theorem coe_sup (s t : Opens Œ±) : (‚Üë(s ‚äî t) : Set Œ±) = ‚Üës ‚à™ ‚Üët :=
  rfl

@[simp, norm_cast]
theorem coe_bot : ((‚ä• : Opens Œ±) : Set Œ±) = ‚àÖ :=
  rfl

@[simp] theorem mk_empty : (‚ü®‚àÖ, isOpen_empty‚ü© : Opens Œ±) = ‚ä• := rfl

@[simp, norm_cast]
theorem coe_eq_empty {U : Opens Œ±} : (U : Set Œ±) = ‚àÖ ‚Üî U = ‚ä• :=
  SetLike.coe_injective.eq_iff' rfl

@[simp]
lemma mem_top (x : Œ±) : x ‚àà (‚ä§ : Opens Œ±) := trivial

@[simp, norm_cast]
theorem coe_top : ((‚ä§ : Opens Œ±) : Set Œ±) = Set.univ :=
  rfl

@[simp] theorem mk_univ : (‚ü®univ, isOpen_univ‚ü© : Opens Œ±) = ‚ä§ := rfl

@[simp, norm_cast]
theorem coe_eq_univ {U : Opens Œ±} : (U : Set Œ±) = univ ‚Üî U = ‚ä§ :=
  SetLike.coe_injective.eq_iff' rfl

@[simp, norm_cast]
theorem coe_sSup {S : Set (Opens Œ±)} : (‚Üë(sSup S) : Set Œ±) = ‚ãÉ i ‚àà S, ‚Üëi :=
  rfl

@[simp, norm_cast]
theorem coe_finset_sup (f : Œπ ‚Üí Opens Œ±) (s : Finset Œπ) : (‚Üë(s.sup f) : Set Œ±) = s.sup ((‚Üë) ‚àò f) :=
  map_finset_sup (‚ü®‚ü®(‚Üë), coe_sup‚ü©, coe_bot‚ü© : SupBotHom (Opens Œ±) (Set Œ±)) _ _

@[simp, norm_cast]
theorem coe_finset_inf (f : Œπ ‚Üí Opens Œ±) (s : Finset Œπ) : (‚Üë(s.inf f) : Set Œ±) = s.inf ((‚Üë) ‚àò f) :=
  map_finset_inf (‚ü®‚ü®(‚Üë), coe_inf‚ü©, coe_top‚ü© : InfTopHom (Opens Œ±) (Set Œ±)) _ _

instance : Inhabited (Opens Œ±) := ‚ü®‚ä•‚ü©

-- porting note (#10754): new instance
instance [IsEmpty Œ±] : Unique (Opens Œ±) where
  uniq _ := ext <| Subsingleton.elim _ _

-- porting note (#10754): new instance
instance [Nonempty Œ±] : Nontrivial (Opens Œ±) where
  exists_pair_ne := ‚ü®‚ä•, ‚ä§, mt coe_inj.2 empty_ne_univ‚ü©

@[simp, norm_cast]
theorem coe_iSup {Œπ} (s : Œπ ‚Üí Opens Œ±) : ((‚®Ü i, s i : Opens Œ±) : Set Œ±) = ‚ãÉ i, s i := by
  simp [iSup]

theorem iSup_def {Œπ} (s : Œπ ‚Üí Opens Œ±) : ‚®Ü i, s i = ‚ü®‚ãÉ i, s i, isOpen_iUnion fun i => (s i).2‚ü© :=
  ext <| coe_iSup s

@[simp]
theorem iSup_mk {Œπ} (s : Œπ ‚Üí Set Œ±) (h : ‚àÄ i, IsOpen (s i)) :
    (‚®Ü i, ‚ü®s i, h i‚ü© : Opens Œ±) = ‚ü®‚ãÉ i, s i, isOpen_iUnion h‚ü© :=
  iSup_def _

@[simp]
theorem mem_iSup {Œπ} {x : Œ±} {s : Œπ ‚Üí Opens Œ±} : x ‚àà iSup s ‚Üî ‚àÉ i, x ‚àà s i := by
  rw [‚Üê SetLike.mem_coe]
  simp

@[simp]
theorem mem_sSup {Us : Set (Opens Œ±)} {x : Œ±} : x ‚àà sSup Us ‚Üî ‚àÉ u ‚àà Us, x ‚àà u := by
  simp_rw [sSup_eq_iSup, mem_iSup, exists_prop]

/-- Open sets in a topological space form a frame. -/
def frameMinimalAxioms : Frame.MinimalAxioms (Opens Œ±) where
  inf_sSup_le_iSup_inf a s :=
    (ext <| by simp only [coe_inf, coe_iSup, coe_sSup, Set.inter_iUnion‚ÇÇ]).le

instance instFrame : Frame (Opens Œ±) := .ofMinimalAxioms frameMinimalAxioms

theorem openEmbedding' (U : Opens Œ±) : OpenEmbedding (Subtype.val : U ‚Üí Œ±) :=
  U.isOpen.openEmbedding_subtype_val

theorem openEmbedding_of_le {U V : Opens Œ±} (i : U ‚â§ V) :
    OpenEmbedding (Set.inclusion <| SetLike.coe_subset_coe.2 i) :=
  { toEmbedding := embedding_inclusion i
    isOpen_range := by
      rw [Set.range_inclusion i]
      exact U.isOpen.preimage continuous_subtype_val }

theorem not_nonempty_iff_eq_bot (U : Opens Œ±) : ¬¨Set.Nonempty (U : Set Œ±) ‚Üî U = ‚ä• := by
  rw [‚Üê coe_inj, coe_bot, ‚Üê Set.not_nonempty_iff_eq_empty]

theorem ne_bot_iff_nonempty (U : Opens Œ±) : U ‚â† ‚ä• ‚Üî Set.Nonempty (U : Set Œ±) := by
  rw [Ne, ‚Üê not_nonempty_iff_eq_bot, not_not]

/-- An open set in the indiscrete topology is either empty or the whole space. -/
theorem eq_bot_or_top {Œ±} [t : TopologicalSpace Œ±] (h : t = ‚ä§) (U : Opens Œ±) : U = ‚ä• ‚à® U = ‚ä§ := by
  subst h; letI : TopologicalSpace Œ± := ‚ä§
  rw [‚Üê coe_eq_empty, ‚Üê coe_eq_univ, ‚Üê isOpen_top_iff]
  exact U.2

-- porting note (#10754): new instance
instance [Nonempty Œ±] [Subsingleton Œ±] : IsSimpleOrder (Opens Œ±) where
  eq_bot_or_eq_top := eq_bot_or_top <| Subsingleton.elim _ _

/-- A set of `opens Œ±` is a basis if the set of corresponding sets is a topological basis. -/
def IsBasis (B : Set (Opens Œ±)) : Prop :=
  IsTopologicalBasis (((‚Üë) : _ ‚Üí Set Œ±) '' B)

theorem isBasis_iff_nbhd {B : Set (Opens Œ±)} :
    IsBasis B ‚Üî ‚àÄ {U : Opens Œ±} {x}, x ‚àà U ‚Üí ‚àÉ U' ‚àà B, x ‚àà U' ‚àß U' ‚â§ U := by
  constructor <;> intro h
  ¬∑ rintro ‚ü®sU, hU‚ü© x hx
    rcases h.mem_nhds_iff.mp (IsOpen.mem_nhds hU hx) with ‚ü®sV, ‚ü®‚ü®V, H‚ÇÅ, H‚ÇÇ‚ü©, hsV‚ü©‚ü©
    refine ‚ü®V, H‚ÇÅ, ?_‚ü©
    cases V
    dsimp at H‚ÇÇ
    subst H‚ÇÇ
    exact hsV
  ¬∑ refine isTopologicalBasis_of_isOpen_of_nhds ?_ ?_
    ¬∑ rintro sU ‚ü®U, -, rfl‚ü©
      exact U.2
    ¬∑ intro x sU hx hsU
      rcases @h ‚ü®sU, hsU‚ü© x hx with ‚ü®V, hV, H‚ü©
      exact ‚ü®V, ‚ü®V, hV, rfl‚ü©, H‚ü©

theorem isBasis_iff_cover {B : Set (Opens Œ±)} :
    IsBasis B ‚Üî ‚àÄ U : Opens Œ±, ‚àÉ Us, Us ‚äÜ B ‚àß U = sSup Us := by
  constructor
  ¬∑ intro hB U
    refine ‚ü®{ V : Opens Œ± | V ‚àà B ‚àß V ‚â§ U }, fun U hU => hU.left, ext ?_‚ü©
    rw [coe_sSup, hB.open_eq_sUnion' U.isOpen]
    simp_rw [sUnion_eq_biUnion, iUnion, mem_setOf_eq, iSup_and, iSup_image]
    rfl
  ¬∑ intro h
    rw [isBasis_iff_nbhd]
    intro U x hx
    rcases h U with ‚ü®Us, hUs, rfl‚ü©
    rcases mem_sSup.1 hx with ‚ü®U, Us, xU‚ü©
    exact ‚ü®U, hUs Us, xU, le_sSup Us‚ü©

/-- If `Œ±` has a basis consisting of compact opens, then an open set in `Œ±` is compact open iff
  it is a finite union of some elements in the basis -/
theorem IsBasis.isCompact_open_iff_eq_finite_iUnion {Œπ : Type*} (b : Œπ ‚Üí Opens Œ±)
    (hb : IsBasis (Set.range b)) (hb' : ‚àÄ i, IsCompact (b i : Set Œ±)) (U : Set Œ±) :
    IsCompact U ‚àß IsOpen U ‚Üî ‚àÉ s : Set Œπ, s.Finite ‚àß U = ‚ãÉ i ‚àà s, b i := by
  apply isCompact_open_iff_eq_finite_iUnion_of_isTopologicalBasis fun i : Œπ => (b i).1
  ¬∑ convert (config := {transparency := .default}) hb
    ext
    simp
  ¬∑ exact hb'

lemma IsBasis.le_iff {Œ±} {t‚ÇÅ t‚ÇÇ : TopologicalSpace Œ±}
    {Us : Set (Opens Œ±)} (hUs : @IsBasis Œ± t‚ÇÇ Us) :
    t‚ÇÅ ‚â§ t‚ÇÇ ‚Üî ‚àÄ U ‚àà Us, IsOpen[t‚ÇÅ] U := by
  conv_lhs => rw [hUs.eq_generateFrom]
  simp [Set.subset_def, le_generateFrom_iff_subset_isOpen]

@[simp]
theorem isCompactElement_iff (s : Opens Œ±) :
    CompleteLattice.IsCompactElement s ‚Üî IsCompact (s : Set Œ±) := by
  rw [isCompact_iff_finite_subcover, CompleteLattice.isCompactElement_iff]
  refine ‚ü®?_, fun H Œπ U hU => ?_‚ü©
  ¬∑ introv H hU hU'
    obtain ‚ü®t, ht‚ü© := H Œπ (fun i => ‚ü®U i, hU i‚ü©) (by simpa)
    refine ‚ü®t, Set.Subset.trans ht ?_‚ü©
    rw [coe_finset_sup, Finset.sup_eq_iSup]
    rfl
  ¬∑ obtain ‚ü®t, ht‚ü© :=
      H (fun i => U i) (fun i => (U i).isOpen) (by simpa using show (s : Set Œ±) ‚äÜ ‚Üë(iSup U) from hU)
    refine ‚ü®t, Set.Subset.trans ht ?_‚ü©
    simp only [Set.iUnion_subset_iff]
    show ‚àÄ i ‚àà t, U i ‚â§ t.sup U
    exact fun i => Finset.le_sup

/-- The preimage of an open set, as an open set. -/
def comap (f : C(Œ±, Œ≤)) : FrameHom (Opens Œ≤) (Opens Œ±) where
  toFun s := ‚ü®f ‚Åª¬π' s, s.2.preimage f.continuous‚ü©
  map_sSup' s := ext <| by simp only [coe_sSup, preimage_iUnion, biUnion_image, coe_mk]
  map_inf' a b := rfl
  map_top' := rfl

@[simp]
theorem comap_id : comap (ContinuousMap.id Œ±) = FrameHom.id _ :=
  FrameHom.ext fun _ => ext rfl

theorem comap_mono (f : C(Œ±, Œ≤)) {s t : Opens Œ≤} (h : s ‚â§ t) : comap f s ‚â§ comap f t :=
  OrderHomClass.mono (comap f) h

@[simp]
theorem coe_comap (f : C(Œ±, Œ≤)) (U : Opens Œ≤) : ‚Üë(comap f U) = f ‚Åª¬π' U :=
  rfl

@[simp]
theorem mem_comap {f : C(Œ±, Œ≤)} {U : Opens Œ≤} {x : Œ±} : x ‚àà comap f U ‚Üî f x ‚àà U := .rfl

protected theorem comap_comp (g : C(Œ≤, Œ≥)) (f : C(Œ±, Œ≤)) :
    comap (g.comp f) = (comap f).comp (comap g) :=
  rfl

protected theorem comap_comap (g : C(Œ≤, Œ≥)) (f : C(Œ±, Œ≤)) (U : Opens Œ≥) :
    comap f (comap g U) = comap (g.comp f) U :=
  rfl

theorem comap_injective [T0Space Œ≤] : Injective (comap : C(Œ±, Œ≤) ‚Üí FrameHom (Opens Œ≤) (Opens Œ±)) :=
  fun f g h =>
  ContinuousMap.ext fun a =>
    Inseparable.eq <|
      inseparable_iff_forall_open.2 fun s hs =>
        have : comap f ‚ü®s, hs‚ü© = comap g ‚ü®s, hs‚ü© := DFunLike.congr_fun h ‚ü®_, hs‚ü©
        show a ‚àà f ‚Åª¬π' s ‚Üî a ‚àà g ‚Åª¬π' s from Set.ext_iff.1 (coe_inj.2 this) a

/-- A homeomorphism induces an order-preserving equivalence on open sets, by taking comaps. -/
@[simps (config := .asFn) apply]
def _root_.Homeomorph.opensCongr (f : Œ± ‚âÉ‚Çú Œ≤) : Opens Œ± ‚âÉo Opens Œ≤ where
  toFun := Opens.comap f.symm.toContinuousMap
  invFun := Opens.comap f.toContinuousMap
  left_inv := fun U => ext <| f.toEquiv.preimage_symm_preimage _
  right_inv := fun U => ext <| f.toEquiv.symm_preimage_preimage _
  map_rel_iff' := by
    simp only [‚Üê SetLike.coe_subset_coe]; exact f.symm.surjective.preimage_subset_preimage_iff

@[simp]
theorem _root_.Homeomorph.opensCongr_symm (f : Œ± ‚âÉ‚Çú Œ≤) : f.opensCongr.symm = f.symm.opensCongr :=
  rfl

instance [Finite Œ±] : Finite (Opens Œ±) :=
  Finite.of_injective _ SetLike.coe_injective

end Opens

/-- The open neighborhoods of a point. See also `Opens` or `nhds`. -/
structure OpenNhdsOf (x : Œ±) extends Opens Œ± where
  /-- The point `x` belongs to every `U : TopologicalSpace.OpenNhdsOf x`. -/
  mem' : x ‚àà carrier

namespace OpenNhdsOf

variable {x : Œ±}

theorem toOpens_injective : Injective (toOpens : OpenNhdsOf x ‚Üí Opens Œ±)
  | ‚ü®_, _‚ü©, ‚ü®_, _‚ü©, rfl => rfl

instance : SetLike (OpenNhdsOf x) Œ± where
  coe U := U.1
  coe_injective' := SetLike.coe_injective.comp toOpens_injective

instance canLiftSet : CanLift (Set Œ±) (OpenNhdsOf x) (‚Üë) fun s => IsOpen s ‚àß x ‚àà s :=
  ‚ü®fun s hs => ‚ü®‚ü®‚ü®s, hs.1‚ü©, hs.2‚ü©, rfl‚ü©‚ü©

protected theorem mem (U : OpenNhdsOf x) : x ‚àà U :=
  U.mem'

protected theorem isOpen (U : OpenNhdsOf x) : IsOpen (U : Set Œ±) :=
  U.is_open'

instance : OrderTop (OpenNhdsOf x) where
  top := ‚ü®‚ä§, Set.mem_univ _‚ü©
  le_top _ := subset_univ _

instance : Inhabited (OpenNhdsOf x) := ‚ü®‚ä§‚ü©
instance : Inf (OpenNhdsOf x) := ‚ü®fun U V => ‚ü®U.1 ‚äì V.1, U.2, V.2‚ü©‚ü©
instance : Sup (OpenNhdsOf x) := ‚ü®fun U V => ‚ü®U.1 ‚äî V.1, Or.inl U.2‚ü©‚ü©

-- porting note (#10754): new instance
instance [Subsingleton Œ±] : Unique (OpenNhdsOf x) where
  uniq U := SetLike.ext' <| Subsingleton.eq_univ_of_nonempty ‚ü®x, U.mem‚ü©

instance : DistribLattice (OpenNhdsOf x) :=
  toOpens_injective.distribLattice _ (fun _ _ => rfl) fun _ _ => rfl

theorem basis_nhds : (ùìù x).HasBasis (fun _ : OpenNhdsOf x => True) (‚Üë) :=
  (nhds_basis_opens x).to_hasBasis (fun U hU => ‚ü®‚ü®‚ü®U, hU.2‚ü©, hU.1‚ü©, trivial, Subset.rfl‚ü©) fun U _ =>
    ‚ü®U, ‚ü®‚ü®U.mem, U.isOpen‚ü©, Subset.rfl‚ü©‚ü©

/-- Preimage of an open neighborhood of `f x` under a continuous map `f` as a `LatticeHom`. -/
def comap (f : C(Œ±, Œ≤)) (x : Œ±) : LatticeHom (OpenNhdsOf (f x)) (OpenNhdsOf x) where
  toFun U := ‚ü®Opens.comap f U.1, U.mem‚ü©
  map_sup' _ _ := rfl
  map_inf' _ _ := rfl

end OpenNhdsOf

end TopologicalSpace

-- Porting note (#11215): TODO: once we port `auto_cases`, port this
-- namespace Tactic

-- namespace AutoCases

-- /-- Find an `auto_cases_tac` which matches `TopologicalSpace.Opens`. -/
-- unsafe def opens_find_tac : expr ‚Üí Option auto_cases_tac
--   | q(TopologicalSpace.Opens _) => tac_cases
--   | _ => none

-- end AutoCases

-- /-- A version of `tactic.auto_cases` that works for `TopologicalSpace.Opens`. -/
-- @[hint_tactic]
-- unsafe def auto_cases_opens : tactic String :=
--   auto_cases tactic.auto_cases.opens_find_tac

-- end Tactic
