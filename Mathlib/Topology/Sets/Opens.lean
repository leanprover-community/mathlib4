/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Floris van Doorn
-/
import Mathlib.Order.Hom.CompleteLattice
import Mathlib.Topology.Compactness.Bases
import Mathlib.Topology.ContinuousMap.Basic
import Mathlib.Order.CompactlyGenerated.Basic
import Mathlib.Order.Copy

/-!
# Open sets

## Summary

We define the subtype of open sets in a topological space.

## Main Definitions

### Bundled open sets

- `TopologicalSpace.Opens Î±` is the type of open subsets of a topological space `Î±`.
- `TopologicalSpace.Opens.IsBasis` is a predicate saying that a set of `Opens`s form a topological
  basis.
- `TopologicalSpace.Opens.comap`: preimage of an open set under a continuous map as a `FrameHom`.
- `Homeomorph.opensCongr`: order-preserving equivalence between open sets in the domain and the
  codomain of a homeomorphism.

### Bundled open neighborhoods

- `TopologicalSpace.OpenNhdsOf x` is the type of open subsets of a topological space `Î±` containing
  `x : Î±`.
- `TopologicalSpace.OpenNhdsOf.comap f x U` is the preimage of open neighborhood `U` of `f x` under
  `f : C(Î±, Î²)`.

## Main results

We define order structures on both `Opens Î±` (`CompleteLattice`, `Frame`) and `OpenNhdsOf x`
(`OrderTop`, `DistribLattice`).

## TODO

- Rename `TopologicalSpace.Opens` to `Open`?
- Port the `auto_cases` tactic version (as a plugin if the ported `auto_cases` will allow plugins).
-/


open Filter Function Order Set

open Topology

variable {Î¹ Î± Î² Î³ : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²] [TopologicalSpace Î³]

namespace TopologicalSpace

variable (Î±) in
/-- The type of open subsets of a topological space. -/
structure Opens where
  /-- The underlying set of a bundled `TopologicalSpace.Opens` object. -/
  carrier : Set Î±
  /-- The `TopologicalSpace.Opens.carrier _` is an open set. -/
  is_open' : IsOpen carrier

namespace Opens

instance : SetLike (Opens Î±) Î± where
  coe := Opens.carrier
  coe_injective' := fun âŸ¨_, _âŸ© âŸ¨_, _âŸ© _ => by congr

instance : CanLift (Set Î±) (Opens Î±) (â†‘) IsOpen :=
  âŸ¨fun s h => âŸ¨âŸ¨s, hâŸ©, rflâŸ©âŸ©

instance instSecondCountableOpens [SecondCountableTopology Î±] (U : Opens Î±) :
    SecondCountableTopology U := inferInstanceAs (SecondCountableTopology U.1)

theorem Â«forallÂ» {p : Opens Î± â†’ Prop} : (âˆ€ U, p U) â†” âˆ€ (U : Set Î±) (hU : IsOpen U), p âŸ¨U, hUâŸ© :=
  âŸ¨fun h _ _ => h _, fun h _ => h _ _âŸ©

@[simp] theorem carrier_eq_coe (U : Opens Î±) : U.1 = â†‘U := rfl

/-- the coercion `Opens Î± â†’ Set Î±` applied to a pair is the same as taking the first component -/
@[simp]
theorem coe_mk {U : Set Î±} {hU : IsOpen U} : â†‘(âŸ¨U, hUâŸ© : Opens Î±) = U :=
  rfl

@[simp]
theorem mem_mk {x : Î±} {U : Set Î±} {h : IsOpen U} : x âˆˆ mk U h â†” x âˆˆ U := Iff.rfl

protected theorem nonempty_coeSort {U : Opens Î±} : Nonempty U â†” (U : Set Î±).Nonempty :=
  Set.nonempty_coe_sort

-- TODO: should this theorem be proved for a `SetLike`?
protected theorem nonempty_coe {U : Opens Î±} : (U : Set Î±).Nonempty â†” âˆƒ x, x âˆˆ U :=
  Iff.rfl

@[ext] -- TODO: replace with `âˆ€ x, x âˆˆ U â†” x âˆˆ V`?
theorem ext {U V : Opens Î±} (h : (U : Set Î±) = V) : U = V :=
  SetLike.coe_injective h

theorem coe_inj {U V : Opens Î±} : (U : Set Î±) = V â†” U = V :=
  SetLike.ext'_iff.symm

/-- A version of `Set.inclusion` not requiring definitional abuse -/
abbrev inclusion {U V : Opens Î±} (h : U â‰¤ V) : U â†’ V := Set.inclusion h

protected theorem isOpen (U : Opens Î±) : IsOpen (U : Set Î±) :=
  U.is_open'

@[simp] theorem mk_coe (U : Opens Î±) : mk (â†‘U) U.isOpen = U := rfl

/-- See Note [custom simps projection]. -/
def Simps.coe (U : Opens Î±) : Set Î± := U

initialize_simps_projections Opens (carrier â†’ coe, as_prefix coe)

/-- The interior of a set, as an element of `Opens`. -/
@[simps]
protected def interior (s : Set Î±) : Opens Î± :=
  âŸ¨interior s, isOpen_interiorâŸ©

@[simp]
theorem mem_interior {s : Set Î±} {x : Î±} : x âˆˆ Opens.interior s â†” x âˆˆ _root_.interior s := .rfl

theorem gc : GaloisConnection ((â†‘) : Opens Î± â†’ Set Î±) Opens.interior := fun U _ =>
  âŸ¨fun h => interior_maximal h U.isOpen, fun h => le_trans h interior_subsetâŸ©

/-- The Galois coinsertion between sets and opens. -/
def gi : GaloisCoinsertion (â†‘) (@Opens.interior Î± _) where
  choice s hs := âŸ¨s, interior_eq_iff_isOpen.mp <| le_antisymm interior_subset hsâŸ©
  gc := gc
  u_l_le _ := interior_subset
  choice_eq _s hs := le_antisymm hs interior_subset

instance : CompleteLattice (Opens Î±) :=
  CompleteLattice.copy (GaloisCoinsertion.liftCompleteLattice gi)
    -- le
    (fun U V => (U : Set Î±) âŠ† V) rfl
    -- top
    âŸ¨univ, isOpen_univâŸ© (ext interior_univ.symm)
    -- bot
    âŸ¨âˆ…, isOpen_emptyâŸ© rfl
    -- sup
    (fun U V => âŸ¨â†‘U âˆª â†‘V, U.2.union V.2âŸ©) rfl
    -- inf
    (fun U V => âŸ¨â†‘U âˆ© â†‘V, U.2.inter V.2âŸ©)
    (funextâ‚‚ fun U V => ext (U.2.inter V.2).interior_eq.symm)
    -- sSup
    (fun S => âŸ¨â‹ƒ s âˆˆ S, â†‘s, isOpen_biUnion fun s _ => s.2âŸ©)
    (funext fun _ => ext sSup_image.symm)
    -- sInf
    _ rfl

@[simp]
theorem mk_inf_mk {U V : Set Î±} {hU : IsOpen U} {hV : IsOpen V} :
    (âŸ¨U, hUâŸ© âŠ“ âŸ¨V, hVâŸ© : Opens Î±) = âŸ¨U âŠ“ V, IsOpen.inter hU hVâŸ© :=
  rfl

@[simp, norm_cast]
theorem coe_inf (s t : Opens Î±) : (â†‘(s âŠ“ t) : Set Î±) = â†‘s âˆ© â†‘t :=
  rfl

@[simp]
lemma mem_inf {s t : Opens Î±} {x : Î±} : x âˆˆ s âŠ“ t â†” x âˆˆ s âˆ§ x âˆˆ t := Iff.rfl

@[simp, norm_cast]
theorem coe_sup (s t : Opens Î±) : (â†‘(s âŠ” t) : Set Î±) = â†‘s âˆª â†‘t :=
  rfl

@[simp, norm_cast]
theorem coe_bot : ((âŠ¥ : Opens Î±) : Set Î±) = âˆ… :=
  rfl

@[simp]
lemma mem_bot {x : Î±} : x âˆˆ (âŠ¥ : Opens Î±) â†” False := Iff.rfl

@[simp] theorem mk_empty : (âŸ¨âˆ…, isOpen_emptyâŸ© : Opens Î±) = âŠ¥ := rfl

@[simp, norm_cast]
theorem coe_eq_empty {U : Opens Î±} : (U : Set Î±) = âˆ… â†” U = âŠ¥ :=
  SetLike.coe_injective.eq_iff' rfl

@[simp]
lemma mem_top (x : Î±) : x âˆˆ (âŠ¤ : Opens Î±) := trivial

@[simp, norm_cast]
theorem coe_top : ((âŠ¤ : Opens Î±) : Set Î±) = Set.univ :=
  rfl

@[simp] theorem mk_univ : (âŸ¨univ, isOpen_univâŸ© : Opens Î±) = âŠ¤ := rfl

@[simp, norm_cast]
theorem coe_eq_univ {U : Opens Î±} : (U : Set Î±) = univ â†” U = âŠ¤ :=
  SetLike.coe_injective.eq_iff' rfl

@[simp, norm_cast]
theorem coe_sSup {S : Set (Opens Î±)} : (â†‘(sSup S) : Set Î±) = â‹ƒ i âˆˆ S, â†‘i :=
  rfl

@[simp, norm_cast]
theorem coe_finset_sup (f : Î¹ â†’ Opens Î±) (s : Finset Î¹) : (â†‘(s.sup f) : Set Î±) = s.sup ((â†‘) âˆ˜ f) :=
  map_finset_sup (âŸ¨âŸ¨(â†‘), coe_supâŸ©, coe_botâŸ© : SupBotHom (Opens Î±) (Set Î±)) _ _

@[simp, norm_cast]
theorem coe_finset_inf (f : Î¹ â†’ Opens Î±) (s : Finset Î¹) : (â†‘(s.inf f) : Set Î±) = s.inf ((â†‘) âˆ˜ f) :=
  map_finset_inf (âŸ¨âŸ¨(â†‘), coe_infâŸ©, coe_topâŸ© : InfTopHom (Opens Î±) (Set Î±)) _ _

instance : Inhabited (Opens Î±) := âŸ¨âŠ¥âŸ©

instance [IsEmpty Î±] : Unique (Opens Î±) where
  uniq _ := ext <| Subsingleton.elim _ _

instance [Nonempty Î±] : Nontrivial (Opens Î±) where
  exists_pair_ne := âŸ¨âŠ¥, âŠ¤, mt coe_inj.2 empty_ne_univâŸ©

@[simp, norm_cast]
theorem coe_iSup {Î¹} (s : Î¹ â†’ Opens Î±) : ((â¨† i, s i : Opens Î±) : Set Î±) = â‹ƒ i, s i := by
  simp [iSup]

theorem iSup_def {Î¹} (s : Î¹ â†’ Opens Î±) : â¨† i, s i = âŸ¨â‹ƒ i, s i, isOpen_iUnion fun i => (s i).2âŸ© :=
  ext <| coe_iSup s

@[simp]
theorem iSup_mk {Î¹} (s : Î¹ â†’ Set Î±) (h : âˆ€ i, IsOpen (s i)) :
    (â¨† i, âŸ¨s i, h iâŸ© : Opens Î±) = âŸ¨â‹ƒ i, s i, isOpen_iUnion hâŸ© :=
  iSup_def _

@[simp]
theorem mem_iSup {Î¹} {x : Î±} {s : Î¹ â†’ Opens Î±} : x âˆˆ iSup s â†” âˆƒ i, x âˆˆ s i := by
  rw [â† SetLike.mem_coe]
  simp

@[simp]
theorem mem_sSup {Us : Set (Opens Î±)} {x : Î±} : x âˆˆ sSup Us â†” âˆƒ u âˆˆ Us, x âˆˆ u := by
  simp_rw [sSup_eq_iSup, mem_iSup, exists_prop]

/-- Open sets in a topological space form a frame. -/
def frameMinimalAxioms : Frame.MinimalAxioms (Opens Î±) where
  inf_sSup_le_iSup_inf a s :=
    (ext <| by simp only [coe_inf, coe_iSup, coe_sSup, Set.inter_iUnionâ‚‚]).le

instance instFrame : Frame (Opens Î±) := .ofMinimalAxioms frameMinimalAxioms

theorem isOpenEmbedding' (U : Opens Î±) : IsOpenEmbedding (Subtype.val : U â†’ Î±) :=
  U.isOpen.isOpenEmbedding_subtypeVal

theorem isOpenEmbedding_of_le {U V : Opens Î±} (i : U â‰¤ V) :
    IsOpenEmbedding (Set.inclusion <| SetLike.coe_subset_coe.2 i) where
  toIsEmbedding := .inclusion i
  isOpen_range := by
    rw [Set.range_inclusion i]
    exact U.isOpen.preimage continuous_subtype_val

theorem not_nonempty_iff_eq_bot (U : Opens Î±) : Â¬Set.Nonempty (U : Set Î±) â†” U = âŠ¥ := by
  rw [â† coe_inj, coe_bot, â† Set.not_nonempty_iff_eq_empty]

theorem ne_bot_iff_nonempty (U : Opens Î±) : U â‰  âŠ¥ â†” Set.Nonempty (U : Set Î±) := by
  rw [Ne, â† not_nonempty_iff_eq_bot, not_not]

/-- An open set in the indiscrete topology is either empty or the whole space. -/
theorem eq_bot_or_top {Î±} [t : TopologicalSpace Î±] (h : t = âŠ¤) (U : Opens Î±) : U = âŠ¥ âˆ¨ U = âŠ¤ := by
  subst h; letI : TopologicalSpace Î± := âŠ¤
  rw [â† coe_eq_empty, â† coe_eq_univ, â† isOpen_top_iff]
  exact U.2

instance [Nonempty Î±] [Subsingleton Î±] : IsSimpleOrder (Opens Î±) where
  eq_bot_or_eq_top := eq_bot_or_top <| Subsingleton.elim _ _

/-- A set of `opens Î±` is a basis if the set of corresponding sets is a topological basis. -/
def IsBasis (B : Set (Opens Î±)) : Prop :=
  IsTopologicalBasis (((â†‘) : _ â†’ Set Î±) '' B)

theorem isBasis_iff_nbhd {B : Set (Opens Î±)} :
    IsBasis B â†” âˆ€ {U : Opens Î±} {x}, x âˆˆ U â†’ âˆƒ U' âˆˆ B, x âˆˆ U' âˆ§ U' â‰¤ U := by
  constructor <;> intro h
  Â· rintro âŸ¨sU, hUâŸ© x hx
    rcases h.mem_nhds_iff.mp (IsOpen.mem_nhds hU hx) with âŸ¨sV, âŸ¨âŸ¨V, Hâ‚, Hâ‚‚âŸ©, hsVâŸ©âŸ©
    refine âŸ¨V, Hâ‚, ?_âŸ©
    cases V
    dsimp at Hâ‚‚
    subst Hâ‚‚
    exact hsV
  Â· refine isTopologicalBasis_of_isOpen_of_nhds ?_ ?_
    Â· rintro sU âŸ¨U, -, rflâŸ©
      exact U.2
    Â· intro x sU hx hsU
      rcases @h âŸ¨sU, hsUâŸ© x hx with âŸ¨V, hV, HâŸ©
      exact âŸ¨V, âŸ¨V, hV, rflâŸ©, HâŸ©

theorem isBasis_iff_cover {B : Set (Opens Î±)} :
    IsBasis B â†” âˆ€ U : Opens Î±, âˆƒ Us, Us âŠ† B âˆ§ U = sSup Us := by
  constructor
  Â· intro hB U
    refine âŸ¨{ V : Opens Î± | V âˆˆ B âˆ§ V â‰¤ U }, fun U hU => hU.left, ext ?_âŸ©
    rw [coe_sSup, hB.open_eq_sUnion' U.isOpen]
    simp_rw [sUnion_eq_biUnion, iUnion, mem_setOf_eq, iSup_and, iSup_image]
    rfl
  Â· intro h
    rw [isBasis_iff_nbhd]
    intro U x hx
    rcases h U with âŸ¨Us, hUs, rflâŸ©
    rcases mem_sSup.1 hx with âŸ¨U, Us, xUâŸ©
    exact âŸ¨U, hUs Us, xU, le_sSup UsâŸ©

/-- If `Î±` has a basis consisting of compact opens, then an open set in `Î±` is compact open iff
  it is a finite union of some elements in the basis -/
theorem IsBasis.isCompact_open_iff_eq_finite_iUnion {Î¹ : Type*} (b : Î¹ â†’ Opens Î±)
    (hb : IsBasis (Set.range b)) (hb' : âˆ€ i, IsCompact (b i : Set Î±)) (U : Set Î±) :
    IsCompact U âˆ§ IsOpen U â†” âˆƒ s : Set Î¹, s.Finite âˆ§ U = â‹ƒ i âˆˆ s, b i := by
  apply isCompact_open_iff_eq_finite_iUnion_of_isTopologicalBasis fun i : Î¹ => (b i).1
  Â· convert (config := {transparency := .default}) hb
    ext
    simp
  Â· exact hb'

lemma IsBasis.exists_finite_of_isCompact {B : Set (Opens Î±)} (hB : IsBasis B) {U : Opens Î±}
    (hU : IsCompact U.1) : âˆƒ Us âŠ† B, Us.Finite âˆ§ U = sSup Us := by
  classical
  obtain âŸ¨Us', hsub, hsupâŸ© := isBasis_iff_cover.mp hB U
  obtain âŸ¨t, htâŸ© := hU.elim_finite_subcover (fun s : Us' â†¦ s.1) (fun s â†¦ s.1.2) (by simp [hsup])
  refine âŸ¨Finset.image Subtype.val t, subset_trans (by simp) hsub, Finset.finite_toSet _, ?_âŸ©
  exact le_antisymm (subset_trans ht (by simp)) (le_trans (sSup_le_sSup (by simp)) hsup.ge)

lemma IsBasis.le_iff {Î±} {tâ‚ tâ‚‚ : TopologicalSpace Î±}
    {Us : Set (Opens Î±)} (hUs : @IsBasis Î± tâ‚‚ Us) :
    tâ‚ â‰¤ tâ‚‚ â†” âˆ€ U âˆˆ Us, IsOpen[tâ‚] U := by
  conv_lhs => rw [hUs.eq_generateFrom]
  simp [Set.subset_def, le_generateFrom_iff_subset_isOpen]

lemma isBasis_sigma {Î¹ : Type*} {Î± : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (Î± i)]
    {B : âˆ€ i, Set (Opens (Î± i))} (hB : âˆ€ i, IsBasis (B i)) :
    IsBasis (â‹ƒ i : Î¹, (fun U â†¦ âŸ¨Sigma.mk i '' U.1, isOpenMap_sigmaMk _ U.2âŸ©) '' B i) := by
  convert TopologicalSpace.IsTopologicalBasis.sigma hB
  simp only [IsBasis, Set.image_iUnion, â† Set.image_comp]
  simp

lemma IsBasis.of_isInducing {B : Set (Opens Î²)} (H : IsBasis B) {f : Î± â†’ Î²} (h : IsInducing f) :
    IsBasis { âŸ¨f â»Â¹' U, U.2.preimage h.continuousâŸ© | U âˆˆ B } := by
  simp only [IsBasis] at H âŠ¢
  convert H.isInducing h
  ext; simp

@[simp]
theorem isCompactElement_iff (s : Opens Î±) :
    CompleteLattice.IsCompactElement s â†” IsCompact (s : Set Î±) := by
  rw [isCompact_iff_finite_subcover, CompleteLattice.isCompactElement_iff]
  refine âŸ¨?_, fun H Î¹ U hU => ?_âŸ©
  Â· introv H hU hU'
    obtain âŸ¨t, htâŸ© := H Î¹ (fun i => âŸ¨U i, hU iâŸ©) (by simpa)
    refine âŸ¨t, Set.Subset.trans ht ?_âŸ©
    rw [coe_finset_sup, Finset.sup_eq_iSup]
    rfl
  Â· obtain âŸ¨t, htâŸ© :=
      H (fun i => U i) (fun i => (U i).isOpen) (by simpa using show (s : Set Î±) âŠ† â†‘(iSup U) from hU)
    refine âŸ¨t, Set.Subset.trans ht ?_âŸ©
    simp only [Set.iUnion_subset_iff]
    change âˆ€ i âˆˆ t, U i â‰¤ t.sup U
    exact fun i => Finset.le_sup

/-- The preimage of an open set, as an open set. -/
def comap (f : C(Î±, Î²)) : FrameHom (Opens Î²) (Opens Î±) where
  toFun s := âŸ¨f â»Â¹' s, s.2.preimage f.continuousâŸ©
  map_sSup' s := ext <| by simp only [coe_sSup, preimage_iUnion, biUnion_image, coe_mk]
  map_inf' _ _ := rfl
  map_top' := rfl

@[simp]
theorem comap_id : comap (ContinuousMap.id Î±) = FrameHom.id _ :=
  FrameHom.ext fun _ => ext rfl

theorem comap_mono (f : C(Î±, Î²)) {s t : Opens Î²} (h : s â‰¤ t) : comap f s â‰¤ comap f t :=
  OrderHomClass.mono (comap f) h

@[simp]
theorem coe_comap (f : C(Î±, Î²)) (U : Opens Î²) : â†‘(comap f U) = f â»Â¹' U :=
  rfl

@[simp]
theorem mem_comap {f : C(Î±, Î²)} {U : Opens Î²} {x : Î±} : x âˆˆ comap f U â†” f x âˆˆ U := .rfl

protected theorem comap_comp (g : C(Î², Î³)) (f : C(Î±, Î²)) :
    comap (g.comp f) = (comap f).comp (comap g) :=
  rfl

protected theorem comap_comap (g : C(Î², Î³)) (f : C(Î±, Î²)) (U : Opens Î³) :
    comap f (comap g U) = comap (g.comp f) U :=
  rfl

theorem comap_injective [T0Space Î²] : Injective (comap : C(Î±, Î²) â†’ FrameHom (Opens Î²) (Opens Î±)) :=
  fun f g h =>
  ContinuousMap.ext fun a =>
    Inseparable.eq <|
      inseparable_iff_forall_isOpen.2 fun s hs =>
        have : comap f âŸ¨s, hsâŸ© = comap g âŸ¨s, hsâŸ© := DFunLike.congr_fun h âŸ¨_, hsâŸ©
        show a âˆˆ f â»Â¹' s â†” a âˆˆ g â»Â¹' s from Set.ext_iff.1 (coe_inj.2 this) a

/-- A homeomorphism induces an order-preserving equivalence on open sets, by taking comaps. -/
@[simps -fullyApplied apply]
def _root_.Homeomorph.opensCongr (f : Î± â‰ƒâ‚œ Î²) : Opens Î± â‰ƒo Opens Î² where
  toFun := Opens.comap (f.symm : C(Î², Î±))
  invFun := Opens.comap (f : C(Î±, Î²))
  left_inv _ := ext <| f.toEquiv.preimage_symm_preimage _
  right_inv _ := ext <| f.toEquiv.symm_preimage_preimage _
  map_rel_iff' := by
    simp only [â† SetLike.coe_subset_coe]; exact f.symm.surjective.preimage_subset_preimage_iff

@[simp]
theorem _root_.Homeomorph.opensCongr_symm (f : Î± â‰ƒâ‚œ Î²) : f.opensCongr.symm = f.symm.opensCongr :=
  rfl

instance [Finite Î±] : Finite (Opens Î±) :=
  Finite.of_injective _ SetLike.coe_injective

end Opens

/-- The open neighborhoods of a point. See also `Opens` or `nhds`. -/
structure OpenNhdsOf (x : Î±) extends Opens Î± where
  /-- The point `x` belongs to every `U : TopologicalSpace.OpenNhdsOf x`. -/
  mem' : x âˆˆ carrier

namespace OpenNhdsOf

variable {x : Î±}

theorem toOpens_injective : Injective (toOpens : OpenNhdsOf x â†’ Opens Î±)
  | âŸ¨_, _âŸ©, âŸ¨_, _âŸ©, rfl => rfl

instance : SetLike (OpenNhdsOf x) Î± where
  coe U := U.1
  coe_injective' := SetLike.coe_injective.comp toOpens_injective

instance canLiftSet : CanLift (Set Î±) (OpenNhdsOf x) (â†‘) fun s => IsOpen s âˆ§ x âˆˆ s :=
  âŸ¨fun s hs => âŸ¨âŸ¨âŸ¨s, hs.1âŸ©, hs.2âŸ©, rflâŸ©âŸ©

protected theorem mem (U : OpenNhdsOf x) : x âˆˆ U :=
  U.mem'

protected theorem isOpen (U : OpenNhdsOf x) : IsOpen (U : Set Î±) :=
  U.is_open'

instance : OrderTop (OpenNhdsOf x) where
  top := âŸ¨âŠ¤, Set.mem_univ _âŸ©
  le_top _ := subset_univ _

instance : Inhabited (OpenNhdsOf x) := âŸ¨âŠ¤âŸ©
instance : Min (OpenNhdsOf x) := âŸ¨fun U V => âŸ¨U.1 âŠ“ V.1, U.2, V.2âŸ©âŸ©
instance : Max (OpenNhdsOf x) := âŸ¨fun U V => âŸ¨U.1 âŠ” V.1, Or.inl U.2âŸ©âŸ©

instance [Subsingleton Î±] : Unique (OpenNhdsOf x) where
  uniq U := SetLike.ext' <| Subsingleton.eq_univ_of_nonempty âŸ¨x, U.memâŸ©

instance : DistribLattice (OpenNhdsOf x) :=
  toOpens_injective.distribLattice _ .rfl .rfl (fun _ _ â†¦ rfl) fun _ _ â†¦ rfl

theorem basis_nhds : (ğ“ x).HasBasis (fun _ : OpenNhdsOf x => True) (â†‘) :=
  (nhds_basis_opens x).to_hasBasis (fun U hU => âŸ¨âŸ¨âŸ¨U, hU.2âŸ©, hU.1âŸ©, trivial, Subset.rflâŸ©) fun U _ =>
    âŸ¨U, âŸ¨âŸ¨U.mem, U.isOpenâŸ©, Subset.rflâŸ©âŸ©

/-- Preimage of an open neighborhood of `f x` under a continuous map `f` as a `LatticeHom`. -/
def comap (f : C(Î±, Î²)) (x : Î±) : LatticeHom (OpenNhdsOf (f x)) (OpenNhdsOf x) where
  toFun U := âŸ¨Opens.comap f U.1, U.memâŸ©
  map_sup' _ _ := rfl
  map_inf' _ _ := rfl

end OpenNhdsOf

end TopologicalSpace

-- Porting note (https://github.com/leanprover-community/mathlib4/issues/11215): TODO: once we port `auto_cases`, port this
-- namespace Tactic

-- namespace AutoCases

-- /-- Find an `auto_cases_tac` which matches `TopologicalSpace.Opens`. -/
-- unsafe def opens_find_tac : expr â†’ Option auto_cases_tac
--   | q(TopologicalSpace.Opens _) => tac_cases
--   | _ => none

-- end AutoCases

-- /-- A version of `tactic.auto_cases` that works for `TopologicalSpace.Opens`. -/
-- @[hint_tactic]
-- unsafe def auto_cases_opens : tactic String :=
--   auto_cases tactic.auto_cases.opens_find_tac

-- end Tactic
