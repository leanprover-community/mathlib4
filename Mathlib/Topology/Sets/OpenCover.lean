/-
Copyright (c) 2025 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
module

public import Mathlib.Topology.Sets.Opens

/-!
# Open covers

We define `IsOpenCover` as a predicate on indexed families of open sets in a topological space `X`,
asserting that their union is `X`. This is an example of a declaration whose name is actually
longer than its content; but giving it a name serves as a way of standardizing API.
-/

@[expose] public section

open Set Topology

namespace TopologicalSpace

/-- An indexed family of open sets whose union is `X`. -/
def IsOpenCover {Œπ X : Type*} [TopologicalSpace X] (u : Œπ ‚Üí Opens X) : Prop :=
  iSup u = ‚ä§

variable {Œπ Œ∫ X Y : Type*} [TopologicalSpace X] {u : Œπ ‚Üí Opens X}
  [TopologicalSpace Y] {v : Œ∫ ‚Üí Opens Y}

namespace IsOpenCover

lemma mk (h : iSup u = ‚ä§) : IsOpenCover u := h

lemma of_sets {v : Œπ ‚Üí Set X} (h_open : ‚àÄ i, IsOpen (v i)) (h_iUnion : ‚ãÉ i, v i = univ) :
    IsOpenCover (fun i ‚Ü¶ ‚ü®v i, h_open i‚ü©) := by
  simp [IsOpenCover, h_iUnion]

lemma iSup_eq_top (hu : IsOpenCover u) : ‚®Ü i, u i = ‚ä§ := hu

lemma iSup_set_eq_univ (hu : IsOpenCover u) : ‚ãÉ i, (u i : Set X) = univ := by
  simpa [‚Üê SetLike.coe_set_eq] using hu.iSup_eq_top

/-- Pullback of a covering of `Y` by a continuous map `X ‚Üí Y`, giving a covering of `X` with the
same index type. -/
lemma comap (hv : IsOpenCover v) (f : C(X, Y)) : IsOpenCover fun k ‚Ü¶ (v k).comap f :=
  by simp [IsOpenCover, ‚Üê preimage_iUnion, hv.iSup_set_eq_univ]

lemma exists_mem (hu : IsOpenCover u) (a : X) : ‚àÉ i, a ‚àà u i := by
  simpa [‚Üê hu.iSup_set_eq_univ] using mem_univ a

lemma exists_mem_nhds (hu : IsOpenCover u) (a : X) : ‚àÉ i, (u i : Set X) ‚àà ùìù a :=
  match hu.exists_mem a with | ‚ü®i, hi‚ü© => ‚ü®i, (u i).isOpen.mem_nhds hi‚ü©

lemma iUnion_inter (hu : IsOpenCover u) (s : Set X) :
    ‚ãÉ i, s ‚à© u i = s := by
  simp [‚Üê inter_iUnion, hu.iSup_set_eq_univ]

lemma isTopologicalBasis (hu : IsOpenCover u)
    {B : ‚àÄ i, Set (Set (u i))} (hB : ‚àÄ i, IsTopologicalBasis (B i)) :
    IsTopologicalBasis (‚ãÉ i, (Subtype.val '' ¬∑) '' B i) :=
  isTopologicalBasis_of_cover (fun i ‚Ü¶ (u i).2) hu.iSup_set_eq_univ hB

end IsOpenCover

section IrreducibleSpace

open Function

/-- Irreducibility can be checked on an open cover with pairwise non-empty intersections. -/
theorem IrreducibleSpace.of_openCover {X Œπ : Type*} [TopologicalSpace X] [hŒπ : Nonempty Œπ]
    {U : Œπ ‚Üí TopologicalSpace.Opens X} (hU : TopologicalSpace.IsOpenCover U)
    (hn : Pairwise ((¬¨ Disjoint ¬∑ ¬∑) on U))
    (h : ‚àÄ i, IrreducibleSpace ‚Ü•(U i)) :
    IrreducibleSpace X := by
  have h' (i : _) : IsIrreducible (U i).carrier :=
    IsIrreducible.of_subtype _
  let i : Œπ := Classical.choice (Œ± := Œπ) hŒπ
  rcases exists_mem_irreducibleComponents_subset_of_isIrreducible (U i).carrier (h' i)
    with ‚ü®u, hu, hUu‚ü©
  by_cases huniv : u = Set.univ
  ¬∑ rw [huniv] at hu
    exact (irreducibleSpace_def _).mpr hu.1
  ¬∑ have huo : IsOpen u·∂ú :=
      IsClosed.isOpen_compl (self := isClosed_of_mem_irreducibleComponents u hu)
    push_neg at huniv
    rw [u.ne_univ_iff_exists_notMem] at huniv
    choose a ha using huniv
    choose j haj using hU.exists_mem a
    have hji : j ‚â† i := fun hji' ‚Ü¶ ha <| hUu <| hji' ‚ñ∏ haj
    rcases Set.inter_nonempty_iff_exists_left.mp
      ((h' j).2 (U i) u·∂ú (U i).isOpen huo
      (not_disjoint_iff_nonempty_inter.mp (by simpa using hn hji)) ‚ü®a, ‚ü®haj, ha‚ü©‚ü©).right
      with ‚ü®x, hx‚ÇÅ, hx‚ÇÇ‚ü©
    exfalso; exact hx‚ÇÇ <| hUu hx‚ÇÅ

end IrreducibleSpace

end TopologicalSpace
