/-
Copyright (c) 2021 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Antoine Chambert-Loir, Anatole Dedecker
-/
import Mathlib.Algebra.GroupWithZero.Indicator
import Mathlib.Topology.Piecewise
import Mathlib.Topology.Instances.ENNReal.Lemmas

/-!
# Semicontinuous maps

A function `f` from a topological space `Œ±` to an ordered space `Œ≤` is lower semicontinuous at a
point `x` if, for any `y < f x`, for any `x'` close enough to `x`, one has `f x' > y`. In other
words, `f` can jump up, but it cannot jump down.

Upper semicontinuous functions are defined similarly.

This file introduces these notions, and a basic API around them mimicking the API for continuous
functions.

## Main definitions and results

We introduce 4 definitions related to lower semicontinuity:
* `LowerSemicontinuousWithinAt f s x`
* `LowerSemicontinuousAt f x`
* `LowerSemicontinuousOn f s`
* `LowerSemicontinuous f`

We build a basic API using dot notation around these notions, and we prove that
* constant functions are lower semicontinuous;
* `indicator s (fun _ ‚Ü¶ y)` is lower semicontinuous when `s` is open and `0 ‚â§ y`,
  or when `s` is closed and `y ‚â§ 0`;
* continuous functions are lower semicontinuous;
* left composition with a continuous monotone functions maps lower semicontinuous functions to lower
  semicontinuous functions. If the function is anti-monotone, it instead maps lower semicontinuous
  functions to upper semicontinuous functions;
* right composition with continuous functions preserves lower and upper semicontinuity;
* a sum of two (or finitely many) lower semicontinuous functions is lower semicontinuous;
* a supremum of a family of lower semicontinuous functions is lower semicontinuous;
* An infinite sum of `‚Ñù‚â•0‚àû`-valued lower semicontinuous functions is lower semicontinuous.

Similar results are stated and proved for upper semicontinuity.

We also prove that a function is continuous if and only if it is both lower and upper
semicontinuous.

We have some equivalent definitions of lower- and upper-semicontinuity (under certain
restrictions on the order on the codomain):
* `lowerSemicontinuous_iff_isOpen_preimage` in a linear order;
* `lowerSemicontinuous_iff_isClosed_preimage` in a linear order;
* `lowerSemicontinuousAt_iff_le_liminf` in a complete linear order;
* `lowerSemicontinuous_iff_isClosed_epigraph` in a linear order with the order
  topology.

## Implementation details

All the nontrivial results for upper semicontinuous functions are deduced from the corresponding
ones for lower semicontinuous functions using `OrderDual`.

## References

* <https://en.wikipedia.org/wiki/Closed_convex_function>
* <https://en.wikipedia.org/wiki/Semi-continuity>

-/


open Topology ENNReal

open Set Function Filter

variable {Œ± : Type*} [TopologicalSpace Œ±] {Œ≤ : Type*} [Preorder Œ≤] {f g : Œ± ‚Üí Œ≤} {x : Œ±}
  {s t : Set Œ±} {y z : Œ≤}

/-! ### Main definitions -/


/-- A real function `f` is lower semicontinuous at `x` within a set `s` if, for any `Œµ > 0`, for all
`x'` close enough to `x` in `s`, then `f x'` is at least `f x - Œµ`. We formulate this in a general
preordered space, using an arbitrary `y < f x` instead of `f x - Œµ`. -/
def LowerSemicontinuousWithinAt (f : Œ± ‚Üí Œ≤) (s : Set Œ±) (x : Œ±) :=
  ‚àÄ y < f x, ‚àÄ·∂† x' in ùìù[s] x, y < f x'

/-- A real function `f` is lower semicontinuous on a set `s` if, for any `Œµ > 0`, for any `x ‚àà s`,
for all `x'` close enough to `x` in `s`, then `f x'` is at least `f x - Œµ`. We formulate this in
a general preordered space, using an arbitrary `y < f x` instead of `f x - Œµ`. -/
def LowerSemicontinuousOn (f : Œ± ‚Üí Œ≤) (s : Set Œ±) :=
  ‚àÄ x ‚àà s, LowerSemicontinuousWithinAt f s x

/-- A real function `f` is lower semicontinuous at `x` if, for any `Œµ > 0`, for all `x'` close
enough to `x`, then `f x'` is at least `f x - Œµ`. We formulate this in a general preordered space,
using an arbitrary `y < f x` instead of `f x - Œµ`. -/
def LowerSemicontinuousAt (f : Œ± ‚Üí Œ≤) (x : Œ±) :=
  ‚àÄ y < f x, ‚àÄ·∂† x' in ùìù x, y < f x'

/-- A real function `f` is lower semicontinuous if, for any `Œµ > 0`, for any `x`, for all `x'` close
enough to `x`, then `f x'` is at least `f x - Œµ`. We formulate this in a general preordered space,
using an arbitrary `y < f x` instead of `f x - Œµ`. -/
def LowerSemicontinuous (f : Œ± ‚Üí Œ≤) :=
  ‚àÄ x, LowerSemicontinuousAt f x

/-- A real function `f` is upper semicontinuous at `x` within a set `s` if, for any `Œµ > 0`, for all
`x'` close enough to `x` in `s`, then `f x'` is at most `f x + Œµ`. We formulate this in a general
preordered space, using an arbitrary `y > f x` instead of `f x + Œµ`. -/
def UpperSemicontinuousWithinAt (f : Œ± ‚Üí Œ≤) (s : Set Œ±) (x : Œ±) :=
  ‚àÄ y, f x < y ‚Üí ‚àÄ·∂† x' in ùìù[s] x, f x' < y

/-- A real function `f` is upper semicontinuous on a set `s` if, for any `Œµ > 0`, for any `x ‚àà s`,
for all `x'` close enough to `x` in `s`, then `f x'` is at most `f x + Œµ`. We formulate this in a
general preordered space, using an arbitrary `y > f x` instead of `f x + Œµ`. -/
def UpperSemicontinuousOn (f : Œ± ‚Üí Œ≤) (s : Set Œ±) :=
  ‚àÄ x ‚àà s, UpperSemicontinuousWithinAt f s x

/-- A real function `f` is upper semicontinuous at `x` if, for any `Œµ > 0`, for all `x'` close
enough to `x`, then `f x'` is at most `f x + Œµ`. We formulate this in a general preordered space,
using an arbitrary `y > f x` instead of `f x + Œµ`. -/
def UpperSemicontinuousAt (f : Œ± ‚Üí Œ≤) (x : Œ±) :=
  ‚àÄ y, f x < y ‚Üí ‚àÄ·∂† x' in ùìù x, f x' < y

/-- A real function `f` is upper semicontinuous if, for any `Œµ > 0`, for any `x`, for all `x'`
close enough to `x`, then `f x'` is at most `f x + Œµ`. We formulate this in a general preordered
space, using an arbitrary `y > f x` instead of `f x + Œµ`. -/
def UpperSemicontinuous (f : Œ± ‚Üí Œ≤) :=
  ‚àÄ x, UpperSemicontinuousAt f x

/-!
### Lower semicontinuous functions
-/


/-! #### Basic dot notation interface for lower semicontinuity -/


theorem LowerSemicontinuousWithinAt.mono (h : LowerSemicontinuousWithinAt f s x) (hst : t ‚äÜ s) :
    LowerSemicontinuousWithinAt f t x := fun y hy =>
  Filter.Eventually.filter_mono (nhdsWithin_mono _ hst) (h y hy)

theorem LowerSemicontinuousWithinAt.congr_of_eventuallyEq {a : Œ±}
    (h : LowerSemicontinuousWithinAt f s a)
    (has : a ‚àà s) (hfg : ‚àÄ·∂† x in nhdsWithin a s, f x = g x) :
    LowerSemicontinuousWithinAt g s a := by
  intro b hb
  rw [‚Üê Filter.EventuallyEq.eq_of_nhdsWithin hfg has] at hb
  exact EventuallyEq.rw hfg _ (h b hb)

theorem lowerSemicontinuousWithinAt_univ_iff :
    LowerSemicontinuousWithinAt f univ x ‚Üî LowerSemicontinuousAt f x := by
  simp [LowerSemicontinuousWithinAt, LowerSemicontinuousAt, nhdsWithin_univ]

theorem LowerSemicontinuousAt.lowerSemicontinuousWithinAt (s : Set Œ±)
    (h : LowerSemicontinuousAt f x) : LowerSemicontinuousWithinAt f s x := fun y hy =>
  Filter.Eventually.filter_mono nhdsWithin_le_nhds (h y hy)

theorem LowerSemicontinuousOn.lowerSemicontinuousWithinAt (h : LowerSemicontinuousOn f s)
    (hx : x ‚àà s) : LowerSemicontinuousWithinAt f s x :=
  h x hx

theorem LowerSemicontinuousOn.mono (h : LowerSemicontinuousOn f s) (hst : t ‚äÜ s) :
    LowerSemicontinuousOn f t := fun x hx => (h x (hst hx)).mono hst

theorem lowerSemicontinuousOn_univ_iff : LowerSemicontinuousOn f univ ‚Üî LowerSemicontinuous f := by
  simp [LowerSemicontinuousOn, LowerSemicontinuous, lowerSemicontinuousWithinAt_univ_iff]

@[simp] theorem lowerSemicontinuous_restrict_iff :
    LowerSemicontinuous (s.restrict f) ‚Üî LowerSemicontinuousOn f s := by
  rw [LowerSemicontinuousOn, LowerSemicontinuous, SetCoe.forall]
  refine forall‚ÇÇ_congr fun a ha ‚Ü¶ forall‚ÇÇ_congr fun b _ ‚Ü¶ ?_
  simp only [nhdsWithin_eq_map_subtype_coe ha, eventually_map, restrict]

theorem LowerSemicontinuous.lowerSemicontinuousAt (h : LowerSemicontinuous f) (x : Œ±) :
    LowerSemicontinuousAt f x :=
  h x

theorem LowerSemicontinuous.lowerSemicontinuousWithinAt (h : LowerSemicontinuous f) (s : Set Œ±)
    (x : Œ±) : LowerSemicontinuousWithinAt f s x :=
  (h x).lowerSemicontinuousWithinAt s

theorem LowerSemicontinuous.lowerSemicontinuousOn (h : LowerSemicontinuous f) (s : Set Œ±) :
    LowerSemicontinuousOn f s := fun x _hx => h.lowerSemicontinuousWithinAt s x

/-! #### Constants -/


theorem lowerSemicontinuousWithinAt_const : LowerSemicontinuousWithinAt (fun _x => z) s x :=
  fun _y hy => Filter.Eventually.of_forall fun _x => hy

theorem lowerSemicontinuousAt_const : LowerSemicontinuousAt (fun _x => z) x := fun _y hy =>
  Filter.Eventually.of_forall fun _x => hy

theorem lowerSemicontinuousOn_const : LowerSemicontinuousOn (fun _x => z) s := fun _x _hx =>
  lowerSemicontinuousWithinAt_const

theorem lowerSemicontinuous_const : LowerSemicontinuous fun _x : Œ± => z := fun _x =>
  lowerSemicontinuousAt_const

/-! ### lower bounds -/

section

variable {Œ± : Type*} [TopologicalSpace Œ±] {Œ≤ : Type*} [LinearOrder Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ±}

/-- A lower semicontinuous function attains its lower bound on a nonempty compact set. -/
theorem LowerSemicontinuousOn.exists_isMinOn {s : Set Œ±} (ne_s : s.Nonempty)
    (hs : IsCompact s) (hf : LowerSemicontinuousOn f s) :
    ‚àÉ a ‚àà s, IsMinOn f s a := by
--  hf.exists_forall_le_of_isCompact ne_s hs
  simp only [isMinOn_iff]
  have _ : Nonempty Œ± := Exists.nonempty ne_s
  have _ : Nonempty s := Nonempty.to_subtype ne_s
  let œÜ : Œ≤ ‚Üí Filter Œ± := fun b ‚Ü¶ ùìü (s ‚à© f ‚Åª¬π' Iic b)
  let ‚Ñ± : Filter Œ± := ‚®Ö a : s, œÜ (f a)
  have : ‚Ñ±.NeBot := by
    apply iInf_neBot_of_directed _ _
    ¬∑ change Directed GE.ge (fun x ‚Ü¶ (œÜ ‚àò (fun (a : s) ‚Ü¶ f ‚Üëa)) x)
      exact Directed.mono_comp GE.ge (fun x y hxy ‚Ü¶
          principal_mono.mpr (inter_subset_inter_right _ (preimage_mono <| Iic_subset_Iic.mpr hxy))
        ) (IsTotal.directed _)
    ¬∑ intro x
      have : (pure x : Filter Œ±) ‚â§ œÜ (f x) := le_principal_iff.mpr ‚ü®x.2, le_refl (f x)‚ü©
      exact neBot_of_le this
  have h‚Ñ±s : ‚Ñ± ‚â§ ùìü s :=
    iInf_le_of_le (Classical.choice inferInstance) (principal_mono.mpr <| inter_subset_left)
  have h‚Ñ± (x) (hx : x ‚àà s) : ‚àÄ·∂† y in ‚Ñ±, f y ‚â§ f x :=
    mem_iInf_of_mem ‚ü®x, hx‚ü© (by apply inter_subset_right)
  obtain ‚ü®a, ha, h‚ü© := hs h‚Ñ±s
  refine ‚ü®a, ha, fun x hx ‚Ü¶ le_of_not_gt fun hxa ‚Ü¶ ?_‚ü©
  let _ : (ùìù a ‚äì ‚Ñ±).NeBot := h
  suffices ‚àÄ·∂† _ in ùìù a ‚äì ‚Ñ±, False by rwa [eventually_const] at this
  filter_upwards [(hf a ha (f x) hxa).filter_mono (inf_le_inf_left _ h‚Ñ±s),
    (h‚Ñ± x hx).filter_mono (inf_le_right : ùìù a ‚äì ‚Ñ± ‚â§ ‚Ñ±)] using fun y h‚ÇÅ h‚ÇÇ ‚Ü¶ not_le_of_gt h‚ÇÅ h‚ÇÇ

/-- A lower semicontinuous function is bounded below on a compact set. -/
theorem LowerSemicontinuousOn.bddBelow_of_isCompact [Nonempty Œ≤] {s : Set Œ±} (hs : IsCompact s)
    (hf : LowerSemicontinuousOn f s) : BddBelow (f '' s) := by
  cases s.eq_empty_or_nonempty with
  | inl h =>
      simp only [h, Set.image_empty]
      exact bddBelow_empty
  | inr h =>
      obtain ‚ü®a, _, has‚ü© := LowerSemicontinuousOn.exists_isMinOn h hs hf
      exact has.bddBelow

end

/-! #### Indicators -/


section

variable [Zero Œ≤]

theorem IsOpen.lowerSemicontinuous_indicator (hs : IsOpen s) (hy : 0 ‚â§ y) :
    LowerSemicontinuous (indicator s fun _x => y) := by
  intro x z hz
  by_cases h : x ‚àà s <;> simp [h] at hz
  ¬∑ filter_upwards [hs.mem_nhds h]
    simp +contextual [hz]
  ¬∑ refine Filter.Eventually.of_forall fun x' => ?_
    by_cases h' : x' ‚àà s <;> simp [h', hz.trans_le hy, hz]

theorem IsOpen.lowerSemicontinuousOn_indicator (hs : IsOpen s) (hy : 0 ‚â§ y) :
    LowerSemicontinuousOn (indicator s fun _x => y) t :=
  (hs.lowerSemicontinuous_indicator hy).lowerSemicontinuousOn t

theorem IsOpen.lowerSemicontinuousAt_indicator (hs : IsOpen s) (hy : 0 ‚â§ y) :
    LowerSemicontinuousAt (indicator s fun _x => y) x :=
  (hs.lowerSemicontinuous_indicator hy).lowerSemicontinuousAt x

theorem IsOpen.lowerSemicontinuousWithinAt_indicator (hs : IsOpen s) (hy : 0 ‚â§ y) :
    LowerSemicontinuousWithinAt (indicator s fun _x => y) t x :=
  (hs.lowerSemicontinuous_indicator hy).lowerSemicontinuousWithinAt t x

theorem IsClosed.lowerSemicontinuous_indicator (hs : IsClosed s) (hy : y ‚â§ 0) :
    LowerSemicontinuous (indicator s fun _x => y) := by
  intro x z hz
  by_cases h : x ‚àà s <;> simp [h] at hz
  ¬∑ refine Filter.Eventually.of_forall fun x' => ?_
    by_cases h' : x' ‚àà s <;> simp [h', hz, hz.trans_le hy]
  ¬∑ filter_upwards [hs.isOpen_compl.mem_nhds h]
    simp +contextual [hz]

theorem IsClosed.lowerSemicontinuousOn_indicator (hs : IsClosed s) (hy : y ‚â§ 0) :
    LowerSemicontinuousOn (indicator s fun _x => y) t :=
  (hs.lowerSemicontinuous_indicator hy).lowerSemicontinuousOn t

theorem IsClosed.lowerSemicontinuousAt_indicator (hs : IsClosed s) (hy : y ‚â§ 0) :
    LowerSemicontinuousAt (indicator s fun _x => y) x :=
  (hs.lowerSemicontinuous_indicator hy).lowerSemicontinuousAt x

theorem IsClosed.lowerSemicontinuousWithinAt_indicator (hs : IsClosed s) (hy : y ‚â§ 0) :
    LowerSemicontinuousWithinAt (indicator s fun _x => y) t x :=
  (hs.lowerSemicontinuous_indicator hy).lowerSemicontinuousWithinAt t x

end

/-! #### Relationship with continuity -/


theorem lowerSemicontinuous_iff_isOpen_preimage :
    LowerSemicontinuous f ‚Üî ‚àÄ y, IsOpen (f ‚Åª¬π' Ioi y) :=
  ‚ü®fun H y => isOpen_iff_mem_nhds.2 fun x hx => H x y hx, fun H _x y y_lt =>
    IsOpen.mem_nhds (H y) y_lt‚ü©

theorem LowerSemicontinuous.isOpen_preimage (hf : LowerSemicontinuous f) (y : Œ≤) :
    IsOpen (f ‚Åª¬π' Ioi y) :=
  lowerSemicontinuous_iff_isOpen_preimage.1 hf y

theorem lowerSemicontinuousOn_iff_preimage_Ioi :
    LowerSemicontinuousOn f s ‚Üî ‚àÄ b, ‚àÉ u, IsOpen u ‚àß s ‚à© f ‚Åª¬π' Set.Ioi b = s ‚à© u := by
  simp only [‚Üê lowerSemicontinuous_restrict_iff, restrict_eq,
    lowerSemicontinuous_iff_isOpen_preimage, preimage_comp, isOpen_induced_iff,
    Subtype.preimage_coe_eq_preimage_coe_iff, eq_comm]

section

variable {Œ≥ : Type*} [LinearOrder Œ≥]

theorem lowerSemicontinuous_iff_isClosed_preimage {f : Œ± ‚Üí Œ≥} :
    LowerSemicontinuous f ‚Üî ‚àÄ y, IsClosed (f ‚Åª¬π' Iic y) := by
  rw [lowerSemicontinuous_iff_isOpen_preimage]
  simp only [‚Üê isOpen_compl_iff, ‚Üê preimage_compl, compl_Iic]

theorem LowerSemicontinuous.isClosed_preimage {f : Œ± ‚Üí Œ≥} (hf : LowerSemicontinuous f) (y : Œ≥) :
    IsClosed (f ‚Åª¬π' Iic y) :=
  lowerSemicontinuous_iff_isClosed_preimage.1 hf y

theorem lowerSemicontinuousOn_iff_preimage_Iic {f : Œ± ‚Üí Œ≥} :
    LowerSemicontinuousOn f s ‚Üî ‚àÄ b, ‚àÉ v, IsClosed v ‚àß s ‚à© f ‚Åª¬π' Set.Iic b = s ‚à© v := by
  simp only [‚Üê lowerSemicontinuous_restrict_iff, restrict_eq,
      lowerSemicontinuous_iff_isClosed_preimage, preimage_comp,
      isClosed_induced_iff, Subtype.preimage_coe_eq_preimage_coe_iff, eq_comm]

variable [TopologicalSpace Œ≥] [OrderTopology Œ≥]

theorem ContinuousWithinAt.lowerSemicontinuousWithinAt {f : Œ± ‚Üí Œ≥} (h : ContinuousWithinAt f s x) :
    LowerSemicontinuousWithinAt f s x := fun _y hy => h (Ioi_mem_nhds hy)

theorem ContinuousAt.lowerSemicontinuousAt {f : Œ± ‚Üí Œ≥} (h : ContinuousAt f x) :
    LowerSemicontinuousAt f x := fun _y hy => h (Ioi_mem_nhds hy)

theorem ContinuousOn.lowerSemicontinuousOn {f : Œ± ‚Üí Œ≥} (h : ContinuousOn f s) :
    LowerSemicontinuousOn f s := fun x hx => (h x hx).lowerSemicontinuousWithinAt

theorem Continuous.lowerSemicontinuous {f : Œ± ‚Üí Œ≥} (h : Continuous f) : LowerSemicontinuous f :=
  fun _x => h.continuousAt.lowerSemicontinuousAt

end

/-! #### Equivalent definitions -/

section

variable {Œ≥ : Type*} [CompleteLinearOrder Œ≥]

theorem lowerSemicontinuousWithinAt_iff_le_liminf {f : Œ± ‚Üí Œ≥} :
    LowerSemicontinuousWithinAt f s x ‚Üî f x ‚â§ liminf f (ùìù[s] x) := by
  constructor
  ¬∑ intro hf; unfold LowerSemicontinuousWithinAt at hf
    contrapose! hf
    obtain ‚ü®z, ltz, y, ylt, h‚ÇÅ‚ü© := hf.exists_disjoint_Iio_Ioi; use y
    exact ‚ü®ylt, fun h => ltz.not_ge
      (le_liminf_of_le (by isBoundedDefault) (h.mono fun _ h‚ÇÇ =>
        le_of_not_gt fun h‚ÇÉ => (h‚ÇÅ _ h‚ÇÉ _ h‚ÇÇ).false))‚ü©
  exact fun hf y ylt => eventually_lt_of_lt_liminf (ylt.trans_le hf)

alias ‚ü®LowerSemicontinuousWithinAt.le_liminf, _‚ü© := lowerSemicontinuousWithinAt_iff_le_liminf

theorem lowerSemicontinuousAt_iff_le_liminf {f : Œ± ‚Üí Œ≥} :
    LowerSemicontinuousAt f x ‚Üî f x ‚â§ liminf f (ùìù x) := by
  rw [‚Üê lowerSemicontinuousWithinAt_univ_iff, lowerSemicontinuousWithinAt_iff_le_liminf,
    ‚Üê nhdsWithin_univ]

alias ‚ü®LowerSemicontinuousAt.le_liminf, _‚ü© := lowerSemicontinuousAt_iff_le_liminf

theorem lowerSemicontinuous_iff_le_liminf {f : Œ± ‚Üí Œ≥} :
    LowerSemicontinuous f ‚Üî ‚àÄ x, f x ‚â§ liminf f (ùìù x) := by
  simp only [‚Üê lowerSemicontinuousAt_iff_le_liminf, LowerSemicontinuous]

alias ‚ü®LowerSemicontinuous.le_liminf, _‚ü© := lowerSemicontinuous_iff_le_liminf

theorem lowerSemicontinuousOn_iff_le_liminf {f : Œ± ‚Üí Œ≥} :
    LowerSemicontinuousOn f s ‚Üî ‚àÄ x ‚àà s, f x ‚â§ liminf f (ùìù[s] x) := by
  simp only [‚Üê lowerSemicontinuousWithinAt_iff_le_liminf, LowerSemicontinuousOn]

alias ‚ü®LowerSemicontinuousOn.le_liminf, _‚ü© := lowerSemicontinuousOn_iff_le_liminf

end

section

variable {Œ≥ : Type*} [LinearOrder Œ≥] [TopologicalSpace Œ≥] [ClosedIciTopology Œ≥]

theorem lowerSemicontinuousOn_iff_isClosed_epigraph {f : Œ± ‚Üí Œ≥} {s : Set Œ±} (hs : IsClosed s) :
    LowerSemicontinuousOn f s ‚Üî IsClosed {p : Œ± √ó Œ≥ | p.1 ‚àà s ‚àß f p.1 ‚â§ p.2} := by
  simp_rw [LowerSemicontinuousOn, LowerSemicontinuousWithinAt, eventually_nhdsWithin_iff,
    ‚Üê isOpen_compl_iff, compl_setOf, isOpen_iff_eventually, mem_setOf, not_and, not_le]
  constructor
  ¬∑ intro hf ‚ü®x, y‚ü© h
    by_cases hx : x ‚àà s
    ¬∑ have ‚ü®y', hy', z, hz, h‚ÇÅ‚ü© := (h hx).exists_disjoint_Iio_Ioi
      filter_upwards [(hf x hx z hz).prodMk_nhds (eventually_lt_nhds hy')]
        with _ ‚ü®h‚ÇÇ, h‚ÇÉ‚ü© h‚ÇÑ using h‚ÇÅ _ h‚ÇÉ _ <| h‚ÇÇ h‚ÇÑ
    ¬∑ filter_upwards [(continuous_fst.tendsto _).eventually (hs.isOpen_compl.eventually_mem hx)]
        with _ h‚ÇÅ h‚ÇÇ using (h‚ÇÅ h‚ÇÇ).elim
  ¬∑ intro hf x _ y hy
    exact ((Continuous.prodMk_left y).tendsto x).eventually (hf (x, y) (fun _ => hy))

theorem lowerSemicontinuous_iff_isClosed_epigraph {f : Œ± ‚Üí Œ≥} :
    LowerSemicontinuous f ‚Üî IsClosed {p : Œ± √ó Œ≥ | f p.1 ‚â§ p.2} := by
  simp [‚Üê lowerSemicontinuousOn_univ_iff, lowerSemicontinuousOn_iff_isClosed_epigraph]

alias ‚ü®LowerSemicontinuous.isClosed_epigraph, _‚ü© := lowerSemicontinuous_iff_isClosed_epigraph

end

/-! ### Composition -/

section

variable {Œ± Œ≤ : Type*} [Preorder Œ≤] {f g : Œ± ‚Üí Œ≤} {s : Set Œ±} {a : Œ±}

variable [TopologicalSpace Œ±]

variable {Œ≥ : Type*} [TopologicalSpace Œ≥] {g : Œ≥ ‚Üí Œ±} {x : Œ≥} {t : Set Œ≥}

theorem LowerSemicontinuousWithinAt.comp
    (hf : LowerSemicontinuousWithinAt f s (g x)) (hg : ContinuousWithinAt g t x)
    (hg' : MapsTo g t s) :
    LowerSemicontinuousWithinAt (f ‚àò g) t x := fun b hb ‚Ü¶
  (hg.tendsto_nhdsWithin hg').eventually (hf b hb)

theorem LowerSemicontinuousAt.comp
    (hf : LowerSemicontinuousAt f (g x)) (hg : ContinuousAt g x) :
    LowerSemicontinuousAt (f ‚àò g) x := fun b hb ‚Ü¶
  hg.eventually (hf b hb)

theorem LowerSemicontinuousOn.comp
    (hf : LowerSemicontinuousOn f s) (hg : ContinuousOn g t) (hg' : MapsTo g t s) :
    LowerSemicontinuousOn (f ‚àò g) t := fun x hx ‚Ü¶
  (hf (g x) (hg' hx)).comp (hg x hx) hg'

theorem LowerSemicontinuous.comp
    (hf : LowerSemicontinuous f) (hg : Continuous g) : LowerSemicontinuous (f ‚àò g) := fun x ‚Ü¶
  (hf (g x)).comp hg.continuousAt

end

section

variable {Œ≥ : Type*} [LinearOrder Œ≥] [TopologicalSpace Œ≥] [OrderTopology Œ≥]
variable {Œ¥ : Type*} [LinearOrder Œ¥] [TopologicalSpace Œ¥] [OrderTopology Œ¥]
variable {Œπ : Type*} [TopologicalSpace Œπ]

theorem ContinuousAt.comp_lowerSemicontinuousWithinAt {g : Œ≥ ‚Üí Œ¥} {f : Œ± ‚Üí Œ≥}
    (hg : ContinuousAt g (f x)) (hf : LowerSemicontinuousWithinAt f s x) (gmon : Monotone g) :
    LowerSemicontinuousWithinAt (g ‚àò f) s x := by
  intro y hy
  by_cases h : ‚àÉ l, l < f x
  ¬∑ obtain ‚ü®z, zlt, hz‚ü© : ‚àÉ z < f x, Ioc z (f x) ‚äÜ g ‚Åª¬π' Ioi y :=
      exists_Ioc_subset_of_mem_nhds (hg (Ioi_mem_nhds hy)) h
    filter_upwards [hf z zlt] with a ha
    calc
      y < g (min (f x) (f a)) := hz (by simp [zlt, ha])
      _ ‚â§ g (f a) := gmon (min_le_right _ _)
  ¬∑ simp only [not_exists, not_lt] at h
    exact Filter.Eventually.of_forall fun a => hy.trans_le (gmon (h (f a)))

theorem ContinuousAt.comp_lowerSemicontinuousAt {g : Œ≥ ‚Üí Œ¥} {f : Œ± ‚Üí Œ≥} (hg : ContinuousAt g (f x))
    (hf : LowerSemicontinuousAt f x) (gmon : Monotone g) : LowerSemicontinuousAt (g ‚àò f) x := by
  simp only [‚Üê lowerSemicontinuousWithinAt_univ_iff] at hf ‚ä¢
  exact hg.comp_lowerSemicontinuousWithinAt hf gmon

theorem Continuous.comp_lowerSemicontinuousOn {g : Œ≥ ‚Üí Œ¥} {f : Œ± ‚Üí Œ≥} (hg : Continuous g)
    (hf : LowerSemicontinuousOn f s) (gmon : Monotone g) : LowerSemicontinuousOn (g ‚àò f) s :=
  fun x hx => hg.continuousAt.comp_lowerSemicontinuousWithinAt (hf x hx) gmon

theorem Continuous.comp_lowerSemicontinuous {g : Œ≥ ‚Üí Œ¥} {f : Œ± ‚Üí Œ≥} (hg : Continuous g)
    (hf : LowerSemicontinuous f) (gmon : Monotone g) : LowerSemicontinuous (g ‚àò f) := fun x =>
  hg.continuousAt.comp_lowerSemicontinuousAt (hf x) gmon

theorem ContinuousAt.comp_lowerSemicontinuousWithinAt_antitone {g : Œ≥ ‚Üí Œ¥} {f : Œ± ‚Üí Œ≥}
    (hg : ContinuousAt g (f x)) (hf : LowerSemicontinuousWithinAt f s x) (gmon : Antitone g) :
    UpperSemicontinuousWithinAt (g ‚àò f) s x :=
  @ContinuousAt.comp_lowerSemicontinuousWithinAt Œ± _ x s Œ≥ _ _ _ Œ¥·µí·µà _ _ _ g f hg hf gmon

theorem ContinuousAt.comp_lowerSemicontinuousAt_antitone {g : Œ≥ ‚Üí Œ¥} {f : Œ± ‚Üí Œ≥}
    (hg : ContinuousAt g (f x)) (hf : LowerSemicontinuousAt f x) (gmon : Antitone g) :
    UpperSemicontinuousAt (g ‚àò f) x :=
  @ContinuousAt.comp_lowerSemicontinuousAt Œ± _ x Œ≥ _ _ _ Œ¥·µí·µà _ _ _ g f hg hf gmon

theorem Continuous.comp_lowerSemicontinuousOn_antitone {g : Œ≥ ‚Üí Œ¥} {f : Œ± ‚Üí Œ≥} (hg : Continuous g)
    (hf : LowerSemicontinuousOn f s) (gmon : Antitone g) : UpperSemicontinuousOn (g ‚àò f) s :=
  fun x hx => hg.continuousAt.comp_lowerSemicontinuousWithinAt_antitone (hf x hx) gmon

theorem Continuous.comp_lowerSemicontinuous_antitone {g : Œ≥ ‚Üí Œ¥} {f : Œ± ‚Üí Œ≥} (hg : Continuous g)
    (hf : LowerSemicontinuous f) (gmon : Antitone g) : UpperSemicontinuous (g ‚àò f) := fun x =>
  hg.continuousAt.comp_lowerSemicontinuousAt_antitone (hf x) gmon

theorem LowerSemicontinuousAt.comp_continuousAt {f : Œ± ‚Üí Œ≤} {g : Œπ ‚Üí Œ±} {x : Œπ}
    (hf : LowerSemicontinuousAt f (g x)) (hg : ContinuousAt g x) :
    LowerSemicontinuousAt (fun x ‚Ü¶ f (g x)) x :=
  fun _ lt ‚Ü¶ hg.eventually (hf _ lt)

theorem LowerSemicontinuousAt.comp_continuousAt_of_eq {f : Œ± ‚Üí Œ≤} {g : Œπ ‚Üí Œ±} {y : Œ±} {x : Œπ}
    (hf : LowerSemicontinuousAt f y) (hg : ContinuousAt g x) (hy : g x = y) :
    LowerSemicontinuousAt (fun x ‚Ü¶ f (g x)) x := by
  rw [‚Üê hy] at hf
  exact comp_continuousAt hf hg

theorem LowerSemicontinuous.comp_continuous {f : Œ± ‚Üí Œ≤} {g : Œπ ‚Üí Œ±}
    (hf : LowerSemicontinuous f) (hg : Continuous g) : LowerSemicontinuous fun x ‚Ü¶ f (g x) :=
  fun x ‚Ü¶ (hf (g x)).comp_continuousAt hg.continuousAt

end

/-! #### Addition -/


section

variable {Œπ : Type*} {Œ≥ : Type*} [AddCommMonoid Œ≥] [LinearOrder Œ≥] [IsOrderedAddMonoid Œ≥]
  [TopologicalSpace Œ≥] [OrderTopology Œ≥]

/-- The sum of two lower semicontinuous functions is lower semicontinuous. Formulated with an
explicit continuity assumption on addition, for application to `EReal`. The unprimed version of
the lemma uses `[ContinuousAdd]`. -/
theorem LowerSemicontinuousWithinAt.add' {f g : Œ± ‚Üí Œ≥} (hf : LowerSemicontinuousWithinAt f s x)
    (hg : LowerSemicontinuousWithinAt g s x)
    (hcont : ContinuousAt (fun p : Œ≥ √ó Œ≥ => p.1 + p.2) (f x, g x)) :
    LowerSemicontinuousWithinAt (fun z => f z + g z) s x := by
  intro y hy
  obtain ‚ü®u, v, u_open, xu, v_open, xv, h‚ü© :
    ‚àÉ u v : Set Œ≥,
      IsOpen u ‚àß f x ‚àà u ‚àß IsOpen v ‚àß g x ‚àà v ‚àß u √óÀ¢ v ‚äÜ { p : Œ≥ √ó Œ≥ | y < p.fst + p.snd } :=
    mem_nhds_prod_iff'.1 (hcont (isOpen_Ioi.mem_nhds hy))
  by_cases hx‚ÇÅ : ‚àÉ l, l < f x
  ¬∑ obtain ‚ü®z‚ÇÅ, z‚ÇÅlt, h‚ÇÅ‚ü© : ‚àÉ z‚ÇÅ < f x, Ioc z‚ÇÅ (f x) ‚äÜ u :=
      exists_Ioc_subset_of_mem_nhds (u_open.mem_nhds xu) hx‚ÇÅ
    by_cases hx‚ÇÇ : ‚àÉ l, l < g x
    ¬∑ obtain ‚ü®z‚ÇÇ, z‚ÇÇlt, h‚ÇÇ‚ü© : ‚àÉ z‚ÇÇ < g x, Ioc z‚ÇÇ (g x) ‚äÜ v :=
        exists_Ioc_subset_of_mem_nhds (v_open.mem_nhds xv) hx‚ÇÇ
      filter_upwards [hf z‚ÇÅ z‚ÇÅlt, hg z‚ÇÇ z‚ÇÇlt] with z h‚ÇÅz h‚ÇÇz
      have A1 : min (f z) (f x) ‚àà u := by
        by_cases H : f z ‚â§ f x
        ¬∑ simpa [H] using h‚ÇÅ ‚ü®h‚ÇÅz, H‚ü©
        ¬∑ simpa [le_of_not_ge H]
      have A2 : min (g z) (g x) ‚àà v := by
        by_cases H : g z ‚â§ g x
        ¬∑ simpa [H] using h‚ÇÇ ‚ü®h‚ÇÇz, H‚ü©
        ¬∑ simpa [le_of_not_ge H]
      have : (min (f z) (f x), min (g z) (g x)) ‚àà u √óÀ¢ v := ‚ü®A1, A2‚ü©
      calc
        y < min (f z) (f x) + min (g z) (g x) := h this
        _ ‚â§ f z + g z := add_le_add (min_le_left _ _) (min_le_left _ _)
    ¬∑ simp only [not_exists, not_lt] at hx‚ÇÇ
      filter_upwards [hf z‚ÇÅ z‚ÇÅlt] with z h‚ÇÅz
      have A1 : min (f z) (f x) ‚àà u := by
        by_cases H : f z ‚â§ f x
        ¬∑ simpa [H] using h‚ÇÅ ‚ü®h‚ÇÅz, H‚ü©
        ¬∑ simpa [le_of_not_ge H]
      have : (min (f z) (f x), g x) ‚àà u √óÀ¢ v := ‚ü®A1, xv‚ü©
      calc
        y < min (f z) (f x) + g x := h this
        _ ‚â§ f z + g z := add_le_add (min_le_left _ _) (hx‚ÇÇ (g z))
  ¬∑ simp only [not_exists, not_lt] at hx‚ÇÅ
    by_cases hx‚ÇÇ : ‚àÉ l, l < g x
    ¬∑ obtain ‚ü®z‚ÇÇ, z‚ÇÇlt, h‚ÇÇ‚ü© : ‚àÉ z‚ÇÇ < g x, Ioc z‚ÇÇ (g x) ‚äÜ v :=
        exists_Ioc_subset_of_mem_nhds (v_open.mem_nhds xv) hx‚ÇÇ
      filter_upwards [hg z‚ÇÇ z‚ÇÇlt] with z h‚ÇÇz
      have A2 : min (g z) (g x) ‚àà v := by
        by_cases H : g z ‚â§ g x
        ¬∑ simpa [H] using h‚ÇÇ ‚ü®h‚ÇÇz, H‚ü©
        ¬∑ simpa [le_of_not_ge H] using h‚ÇÇ ‚ü®z‚ÇÇlt, le_rfl‚ü©
      have : (f x, min (g z) (g x)) ‚àà u √óÀ¢ v := ‚ü®xu, A2‚ü©
      calc
        y < f x + min (g z) (g x) := h this
        _ ‚â§ f z + g z := add_le_add (hx‚ÇÅ (f z)) (min_le_left _ _)
    ¬∑ simp only [not_exists, not_lt] at hx‚ÇÅ hx‚ÇÇ
      apply Filter.Eventually.of_forall
      intro z
      have : (f x, g x) ‚àà u √óÀ¢ v := ‚ü®xu, xv‚ü©
      calc
        y < f x + g x := h this
        _ ‚â§ f z + g z := add_le_add (hx‚ÇÅ (f z)) (hx‚ÇÇ (g z))

/-- The sum of two lower semicontinuous functions is lower semicontinuous. Formulated with an
explicit continuity assumption on addition, for application to `EReal`. The unprimed version of
the lemma uses `[ContinuousAdd]`. -/
theorem LowerSemicontinuousAt.add' {f g : Œ± ‚Üí Œ≥} (hf : LowerSemicontinuousAt f x)
    (hg : LowerSemicontinuousAt g x)
    (hcont : ContinuousAt (fun p : Œ≥ √ó Œ≥ => p.1 + p.2) (f x, g x)) :
    LowerSemicontinuousAt (fun z => f z + g z) x := by
  simp_rw [‚Üê lowerSemicontinuousWithinAt_univ_iff] at *
  exact hf.add' hg hcont

/-- The sum of two lower semicontinuous functions is lower semicontinuous. Formulated with an
explicit continuity assumption on addition, for application to `EReal`. The unprimed version of
the lemma uses `[ContinuousAdd]`. -/
theorem LowerSemicontinuousOn.add' {f g : Œ± ‚Üí Œ≥} (hf : LowerSemicontinuousOn f s)
    (hg : LowerSemicontinuousOn g s)
    (hcont : ‚àÄ x ‚àà s, ContinuousAt (fun p : Œ≥ √ó Œ≥ => p.1 + p.2) (f x, g x)) :
    LowerSemicontinuousOn (fun z => f z + g z) s := fun x hx =>
  (hf x hx).add' (hg x hx) (hcont x hx)

/-- The sum of two lower semicontinuous functions is lower semicontinuous. Formulated with an
explicit continuity assumption on addition, for application to `EReal`. The unprimed version of
the lemma uses `[ContinuousAdd]`. -/
theorem LowerSemicontinuous.add' {f g : Œ± ‚Üí Œ≥} (hf : LowerSemicontinuous f)
    (hg : LowerSemicontinuous g)
    (hcont : ‚àÄ x, ContinuousAt (fun p : Œ≥ √ó Œ≥ => p.1 + p.2) (f x, g x)) :
    LowerSemicontinuous fun z => f z + g z := fun x => (hf x).add' (hg x) (hcont x)

variable [ContinuousAdd Œ≥]

/-- The sum of two lower semicontinuous functions is lower semicontinuous. Formulated with
`[ContinuousAdd]`. The primed version of the lemma uses an explicit continuity assumption on
addition, for application to `EReal`. -/
theorem LowerSemicontinuousWithinAt.add {f g : Œ± ‚Üí Œ≥} (hf : LowerSemicontinuousWithinAt f s x)
    (hg : LowerSemicontinuousWithinAt g s x) :
    LowerSemicontinuousWithinAt (fun z => f z + g z) s x :=
  hf.add' hg continuous_add.continuousAt

/-- The sum of two lower semicontinuous functions is lower semicontinuous. Formulated with
`[ContinuousAdd]`. The primed version of the lemma uses an explicit continuity assumption on
addition, for application to `EReal`. -/
theorem LowerSemicontinuousAt.add {f g : Œ± ‚Üí Œ≥} (hf : LowerSemicontinuousAt f x)
    (hg : LowerSemicontinuousAt g x) : LowerSemicontinuousAt (fun z => f z + g z) x :=
  hf.add' hg continuous_add.continuousAt

/-- The sum of two lower semicontinuous functions is lower semicontinuous. Formulated with
`[ContinuousAdd]`. The primed version of the lemma uses an explicit continuity assumption on
addition, for application to `EReal`. -/
theorem LowerSemicontinuousOn.add {f g : Œ± ‚Üí Œ≥} (hf : LowerSemicontinuousOn f s)
    (hg : LowerSemicontinuousOn g s) : LowerSemicontinuousOn (fun z => f z + g z) s :=
  hf.add' hg fun _x _hx => continuous_add.continuousAt

/-- The sum of two lower semicontinuous functions is lower semicontinuous. Formulated with
`[ContinuousAdd]`. The primed version of the lemma uses an explicit continuity assumption on
addition, for application to `EReal`. -/
theorem LowerSemicontinuous.add {f g : Œ± ‚Üí Œ≥} (hf : LowerSemicontinuous f)
    (hg : LowerSemicontinuous g) : LowerSemicontinuous fun z => f z + g z :=
  hf.add' hg fun _x => continuous_add.continuousAt

theorem lowerSemicontinuousWithinAt_sum {f : Œπ ‚Üí Œ± ‚Üí Œ≥} {a : Finset Œπ}
    (ha : ‚àÄ i ‚àà a, LowerSemicontinuousWithinAt (f i) s x) :
    LowerSemicontinuousWithinAt (fun z => ‚àë i ‚àà a, f i z) s x := by
  classical
    induction a using Finset.induction_on with
    | empty => exact lowerSemicontinuousWithinAt_const
    | insert _ _ ia IH =>
      simp only [ia, Finset.sum_insert, not_false_iff]
      exact
        LowerSemicontinuousWithinAt.add (ha _ (Finset.mem_insert_self ..))
          (IH fun j ja => ha j (Finset.mem_insert_of_mem ja))

theorem lowerSemicontinuousAt_sum {f : Œπ ‚Üí Œ± ‚Üí Œ≥} {a : Finset Œπ}
    (ha : ‚àÄ i ‚àà a, LowerSemicontinuousAt (f i) x) :
    LowerSemicontinuousAt (fun z => ‚àë i ‚àà a, f i z) x := by
  simp_rw [‚Üê lowerSemicontinuousWithinAt_univ_iff] at *
  exact lowerSemicontinuousWithinAt_sum ha

theorem lowerSemicontinuousOn_sum {f : Œπ ‚Üí Œ± ‚Üí Œ≥} {a : Finset Œπ}
    (ha : ‚àÄ i ‚àà a, LowerSemicontinuousOn (f i) s) :
    LowerSemicontinuousOn (fun z => ‚àë i ‚àà a, f i z) s := fun x hx =>
  lowerSemicontinuousWithinAt_sum fun i hi => ha i hi x hx

theorem lowerSemicontinuous_sum {f : Œπ ‚Üí Œ± ‚Üí Œ≥} {a : Finset Œπ}
    (ha : ‚àÄ i ‚àà a, LowerSemicontinuous (f i)) : LowerSemicontinuous fun z => ‚àë i ‚àà a, f i z :=
  fun x => lowerSemicontinuousAt_sum fun i hi => ha i hi x

end

/-! #### Supremum -/

section

variable {Œ± : Type*} {Œ≤ : Type*} [TopologicalSpace Œ±] [LinearOrder Œ≤]
  {f g : Œ± ‚Üí Œ≤} {s : Set Œ±} {a : Œ±}

theorem LowerSemicontinuousWithinAt.sup
    (hf : LowerSemicontinuousWithinAt f s a) (hg : LowerSemicontinuousWithinAt g s a) :
    LowerSemicontinuousWithinAt (fun x ‚Ü¶ f x ‚äî g x) s a := by
  intro b hb
  simp only [lt_sup_iff] at hb ‚ä¢
  rcases hb with hb | hb
  ¬∑ filter_upwards [hf b hb] with x using Or.intro_left _
  ¬∑ filter_upwards [hg b hb] with x using Or.intro_right _

theorem LowerSemicontinuousAt.sup
    (hf : LowerSemicontinuousAt f a) (hg : LowerSemicontinuousAt g a) :
    LowerSemicontinuousAt (fun x ‚Ü¶ f x ‚äî g x) a := by
  rw [‚Üê lowerSemicontinuousWithinAt_univ_iff] at *
  exact hf.sup hg

theorem LowerSemicontinuousOn.sup
    (hf : LowerSemicontinuousOn f s) (hg : LowerSemicontinuousOn g s) :
    LowerSemicontinuousOn (fun x ‚Ü¶ f x ‚äî g x) s := fun a ha ‚Ü¶
  (hf a ha).sup (hg a ha)

theorem LowerSemicontinuous.sup
    (hf : LowerSemicontinuous f) (hg : LowerSemicontinuous g) :
    LowerSemicontinuous fun x ‚Ü¶ f x ‚äî g x := fun a ‚Ü¶
  (hf a).sup (hg a)

theorem LowerSemicontinuousWithinAt.inf
    (hf : LowerSemicontinuousWithinAt f s a) (hg : LowerSemicontinuousWithinAt g s a) :
    LowerSemicontinuousWithinAt (fun x ‚Ü¶ f x ‚äì g x) s a := by
  intro b hb
  simp only [lt_inf_iff] at hb ‚ä¢
  exact Eventually.and (hf b hb.1) (hg b hb.2)

theorem LowerSemicontinuousAt.inf
    (hf : LowerSemicontinuousAt f a) (hg : LowerSemicontinuousAt g a) :
    LowerSemicontinuousAt (fun x ‚Ü¶ f x ‚äì g x) a := by
  rw [‚Üê lowerSemicontinuousWithinAt_univ_iff] at *
  exact hf.inf hg

theorem LowerSemicontinuousOn.inf
    (hf : LowerSemicontinuousOn f s) (hg : LowerSemicontinuousOn g s) :
    LowerSemicontinuousOn (fun x ‚Ü¶ f x ‚äì g x) s := fun a ha ‚Ü¶
  (hf a ha).inf (hg a ha)

theorem LowerSemicontinuous.inf (hf : LowerSemicontinuous f)
    (hg : LowerSemicontinuous g) :
    LowerSemicontinuous fun x ‚Ü¶ f x ‚äì g x := fun a ‚Ü¶
  (hf a).inf (hg a)

variable {Œπ : Type*} {f : Œπ ‚Üí Œ± ‚Üí Œ≤} {a : Œ±} {I : Set Œπ}

end

section

variable {Œπ : Sort*} {Œ¥ Œ¥' : Type*} [CompleteLinearOrder Œ¥] [ConditionallyCompleteLinearOrder Œ¥']

theorem lowerSemicontinuousWithinAt_ciSup {f : Œπ ‚Üí Œ± ‚Üí Œ¥'}
    (bdd : ‚àÄ·∂† y in ùìù[s] x, BddAbove (range fun i => f i y))
    (h : ‚àÄ i, LowerSemicontinuousWithinAt (f i) s x) :
    LowerSemicontinuousWithinAt (fun x' => ‚®Ü i, f i x') s x := by
  cases isEmpty_or_nonempty Œπ
  ¬∑ simpa only [iSup_of_empty'] using lowerSemicontinuousWithinAt_const
  ¬∑ intro y hy
    rcases exists_lt_of_lt_ciSup hy with ‚ü®i, hi‚ü©
    filter_upwards [h i y hi, bdd] with y hy hy' using hy.trans_le (le_ciSup hy' i)

theorem lowerSemicontinuousWithinAt_iSup {f : Œπ ‚Üí Œ± ‚Üí Œ¥}
    (h : ‚àÄ i, LowerSemicontinuousWithinAt (f i) s x) :
    LowerSemicontinuousWithinAt (fun x' => ‚®Ü i, f i x') s x :=
  lowerSemicontinuousWithinAt_ciSup (by simp) h

theorem lowerSemicontinuousWithinAt_biSup {p : Œπ ‚Üí Prop} {f : ‚àÄ i, p i ‚Üí Œ± ‚Üí Œ¥}
    (h : ‚àÄ i hi, LowerSemicontinuousWithinAt (f i hi) s x) :
    LowerSemicontinuousWithinAt (fun x' => ‚®Ü (i) (hi), f i hi x') s x :=
  lowerSemicontinuousWithinAt_iSup fun i => lowerSemicontinuousWithinAt_iSup fun hi => h i hi

theorem lowerSemicontinuousAt_ciSup {f : Œπ ‚Üí Œ± ‚Üí Œ¥'}
    (bdd : ‚àÄ·∂† y in ùìù x, BddAbove (range fun i => f i y)) (h : ‚àÄ i, LowerSemicontinuousAt (f i) x) :
    LowerSemicontinuousAt (fun x' => ‚®Ü i, f i x') x := by
  simp_rw [‚Üê lowerSemicontinuousWithinAt_univ_iff] at *
  rw [‚Üê nhdsWithin_univ] at bdd
  exact lowerSemicontinuousWithinAt_ciSup bdd h

theorem lowerSemicontinuousAt_iSup {f : Œπ ‚Üí Œ± ‚Üí Œ¥} (h : ‚àÄ i, LowerSemicontinuousAt (f i) x) :
    LowerSemicontinuousAt (fun x' => ‚®Ü i, f i x') x :=
  lowerSemicontinuousAt_ciSup (by simp) h

theorem lowerSemicontinuousAt_biSup {p : Œπ ‚Üí Prop} {f : ‚àÄ i, p i ‚Üí Œ± ‚Üí Œ¥}
    (h : ‚àÄ i hi, LowerSemicontinuousAt (f i hi) x) :
    LowerSemicontinuousAt (fun x' => ‚®Ü (i) (hi), f i hi x') x :=
  lowerSemicontinuousAt_iSup fun i => lowerSemicontinuousAt_iSup fun hi => h i hi

theorem lowerSemicontinuousOn_ciSup {f : Œπ ‚Üí Œ± ‚Üí Œ¥'}
    (bdd : ‚àÄ x ‚àà s, BddAbove (range fun i => f i x)) (h : ‚àÄ i, LowerSemicontinuousOn (f i) s) :
    LowerSemicontinuousOn (fun x' => ‚®Ü i, f i x') s := fun x hx =>
  lowerSemicontinuousWithinAt_ciSup (eventually_nhdsWithin_of_forall bdd) fun i => h i x hx

theorem lowerSemicontinuousOn_iSup {f : Œπ ‚Üí Œ± ‚Üí Œ¥} (h : ‚àÄ i, LowerSemicontinuousOn (f i) s) :
    LowerSemicontinuousOn (fun x' => ‚®Ü i, f i x') s :=
  lowerSemicontinuousOn_ciSup (by simp) h

theorem lowerSemicontinuousOn_biSup {p : Œπ ‚Üí Prop} {f : ‚àÄ i, p i ‚Üí Œ± ‚Üí Œ¥}
    (h : ‚àÄ i hi, LowerSemicontinuousOn (f i hi) s) :
    LowerSemicontinuousOn (fun x' => ‚®Ü (i) (hi), f i hi x') s :=
  lowerSemicontinuousOn_iSup fun i => lowerSemicontinuousOn_iSup fun hi => h i hi

theorem lowerSemicontinuous_ciSup {f : Œπ ‚Üí Œ± ‚Üí Œ¥'} (bdd : ‚àÄ x, BddAbove (range fun i => f i x))
    (h : ‚àÄ i, LowerSemicontinuous (f i)) : LowerSemicontinuous fun x' => ‚®Ü i, f i x' := fun x =>
  lowerSemicontinuousAt_ciSup (Eventually.of_forall bdd) fun i => h i x

theorem lowerSemicontinuous_iSup {f : Œπ ‚Üí Œ± ‚Üí Œ¥} (h : ‚àÄ i, LowerSemicontinuous (f i)) :
    LowerSemicontinuous fun x' => ‚®Ü i, f i x' :=
  lowerSemicontinuous_ciSup (by simp) h

theorem lowerSemicontinuous_biSup {p : Œπ ‚Üí Prop} {f : ‚àÄ i, p i ‚Üí Œ± ‚Üí Œ¥}
    (h : ‚àÄ i hi, LowerSemicontinuous (f i hi)) :
    LowerSemicontinuous fun x' => ‚®Ü (i) (hi), f i hi x' :=
  lowerSemicontinuous_iSup fun i => lowerSemicontinuous_iSup fun hi => h i hi

end

/-! #### Infinite sums -/


section

variable {Œπ : Type*}

theorem lowerSemicontinuousWithinAt_tsum {f : Œπ ‚Üí Œ± ‚Üí ‚Ñù‚â•0‚àû}
    (h : ‚àÄ i, LowerSemicontinuousWithinAt (f i) s x) :
    LowerSemicontinuousWithinAt (fun x' => ‚àë' i, f i x') s x := by
  simp_rw [ENNReal.tsum_eq_iSup_sum]
  refine lowerSemicontinuousWithinAt_iSup fun b => ?_
  exact lowerSemicontinuousWithinAt_sum fun i _hi => h i

theorem lowerSemicontinuousAt_tsum {f : Œπ ‚Üí Œ± ‚Üí ‚Ñù‚â•0‚àû} (h : ‚àÄ i, LowerSemicontinuousAt (f i) x) :
    LowerSemicontinuousAt (fun x' => ‚àë' i, f i x') x := by
  simp_rw [‚Üê lowerSemicontinuousWithinAt_univ_iff] at *
  exact lowerSemicontinuousWithinAt_tsum h

theorem lowerSemicontinuousOn_tsum {f : Œπ ‚Üí Œ± ‚Üí ‚Ñù‚â•0‚àû} (h : ‚àÄ i, LowerSemicontinuousOn (f i) s) :
    LowerSemicontinuousOn (fun x' => ‚àë' i, f i x') s := fun x hx =>
  lowerSemicontinuousWithinAt_tsum fun i => h i x hx

theorem lowerSemicontinuous_tsum {f : Œπ ‚Üí Œ± ‚Üí ‚Ñù‚â•0‚àû} (h : ‚àÄ i, LowerSemicontinuous (f i)) :
    LowerSemicontinuous fun x' => ‚àë' i, f i x' := fun x => lowerSemicontinuousAt_tsum fun i => h i x

end

/-!
### Upper semicontinuous functions
-/


/-! #### Basic dot notation interface for upper semicontinuity -/


theorem UpperSemicontinuousWithinAt.mono (h : UpperSemicontinuousWithinAt f s x) (hst : t ‚äÜ s) :
    UpperSemicontinuousWithinAt f t x := fun y hy =>
  Filter.Eventually.filter_mono (nhdsWithin_mono _ hst) (h y hy)

theorem UpperSemicontinuousWithinAt.congr_of_eventuallyEq {a : Œ±}
    (h : UpperSemicontinuousWithinAt f s a)
    (has : a ‚àà s) (hfg : ‚àÄ·∂† x in nhdsWithin a s, f x = g x) :
    UpperSemicontinuousWithinAt g s a :=
  LowerSemicontinuousWithinAt.congr_of_eventuallyEq (Œ≤ := Œ≤·µí·µà) h has hfg

theorem upperSemicontinuousWithinAt_univ_iff :
    UpperSemicontinuousWithinAt f univ x ‚Üî UpperSemicontinuousAt f x := by
  simp [UpperSemicontinuousWithinAt, UpperSemicontinuousAt, nhdsWithin_univ]

theorem UpperSemicontinuousAt.upperSemicontinuousWithinAt (s : Set Œ±)
    (h : UpperSemicontinuousAt f x) : UpperSemicontinuousWithinAt f s x := fun y hy =>
  Filter.Eventually.filter_mono nhdsWithin_le_nhds (h y hy)

theorem UpperSemicontinuousOn.upperSemicontinuousWithinAt (h : UpperSemicontinuousOn f s)
    (hx : x ‚àà s) : UpperSemicontinuousWithinAt f s x :=
  h x hx

theorem UpperSemicontinuousOn.mono (h : UpperSemicontinuousOn f s) (hst : t ‚äÜ s) :
    UpperSemicontinuousOn f t := fun x hx => (h x (hst hx)).mono hst

theorem upperSemicontinuousOn_univ_iff : UpperSemicontinuousOn f univ ‚Üî UpperSemicontinuous f := by
  simp [UpperSemicontinuousOn, UpperSemicontinuous, upperSemicontinuousWithinAt_univ_iff]

theorem UpperSemicontinuous.upperSemicontinuousAt (h : UpperSemicontinuous f) (x : Œ±) :
    UpperSemicontinuousAt f x :=
  h x

theorem UpperSemicontinuous.upperSemicontinuousWithinAt (h : UpperSemicontinuous f) (s : Set Œ±)
    (x : Œ±) : UpperSemicontinuousWithinAt f s x :=
  (h x).upperSemicontinuousWithinAt s

theorem UpperSemicontinuous.upperSemicontinuousOn (h : UpperSemicontinuous f) (s : Set Œ±) :
    UpperSemicontinuousOn f s := fun x _hx => h.upperSemicontinuousWithinAt s x

/-! #### Constants -/


theorem upperSemicontinuousWithinAt_const : UpperSemicontinuousWithinAt (fun _x => z) s x :=
  fun _y hy => Filter.Eventually.of_forall fun _x => hy

theorem upperSemicontinuousAt_const : UpperSemicontinuousAt (fun _x => z) x := fun _y hy =>
  Filter.Eventually.of_forall fun _x => hy

theorem upperSemicontinuousOn_const : UpperSemicontinuousOn (fun _x => z) s := fun _x _hx =>
  upperSemicontinuousWithinAt_const

theorem upperSemicontinuous_const : UpperSemicontinuous fun _x : Œ± => z := fun _x =>
  upperSemicontinuousAt_const

/-! ### upper bounds -/

section

variable {Œ± : Type*} [TopologicalSpace Œ±] {Œ≤ : Type*} [LinearOrder Œ≤] {f : Œ± ‚Üí Œ≤} {s : Set Œ±}

/-- An upper semicontinuous function attains its upper bound on a nonempty compact set. -/
theorem UpperSemicontinuousOn.exists_isMaxOn {s : Set Œ±} (ne_s : s.Nonempty)
    (hs : IsCompact s) (hf : UpperSemicontinuousOn f s) :
    ‚àÉ a ‚àà s, IsMaxOn f s a :=
  LowerSemicontinuousOn.exists_isMinOn (Œ≤ := Œ≤·µí·µà) ne_s hs hf

/-- An upper semicontinuous function is bounded above on a compact set. -/
theorem UpperSemicontinuousOn.bddAbove_of_isCompact [Nonempty Œ≤] {s : Set Œ±}
    (hs : IsCompact s) (hf : UpperSemicontinuousOn f s) : BddAbove (f '' s) :=
  LowerSemicontinuousOn.bddBelow_of_isCompact (Œ≤ := Œ≤·µí·µà) hs hf

end

/-! #### Indicators -/


section

variable [Zero Œ≤]

theorem IsOpen.upperSemicontinuous_indicator (hs : IsOpen s) (hy : y ‚â§ 0) :
    UpperSemicontinuous (indicator s fun _x => y) :=
  @IsOpen.lowerSemicontinuous_indicator Œ± _ Œ≤·µí·µà _ s y _ hs hy

theorem IsOpen.upperSemicontinuousOn_indicator (hs : IsOpen s) (hy : y ‚â§ 0) :
    UpperSemicontinuousOn (indicator s fun _x => y) t :=
  (hs.upperSemicontinuous_indicator hy).upperSemicontinuousOn t

theorem IsOpen.upperSemicontinuousAt_indicator (hs : IsOpen s) (hy : y ‚â§ 0) :
    UpperSemicontinuousAt (indicator s fun _x => y) x :=
  (hs.upperSemicontinuous_indicator hy).upperSemicontinuousAt x

theorem IsOpen.upperSemicontinuousWithinAt_indicator (hs : IsOpen s) (hy : y ‚â§ 0) :
    UpperSemicontinuousWithinAt (indicator s fun _x => y) t x :=
  (hs.upperSemicontinuous_indicator hy).upperSemicontinuousWithinAt t x

theorem IsClosed.upperSemicontinuous_indicator (hs : IsClosed s) (hy : 0 ‚â§ y) :
    UpperSemicontinuous (indicator s fun _x => y) :=
  @IsClosed.lowerSemicontinuous_indicator Œ± _ Œ≤·µí·µà _ s y _ hs hy

theorem IsClosed.upperSemicontinuousOn_indicator (hs : IsClosed s) (hy : 0 ‚â§ y) :
    UpperSemicontinuousOn (indicator s fun _x => y) t :=
  (hs.upperSemicontinuous_indicator hy).upperSemicontinuousOn t

theorem IsClosed.upperSemicontinuousAt_indicator (hs : IsClosed s) (hy : 0 ‚â§ y) :
    UpperSemicontinuousAt (indicator s fun _x => y) x :=
  (hs.upperSemicontinuous_indicator hy).upperSemicontinuousAt x

theorem IsClosed.upperSemicontinuousWithinAt_indicator (hs : IsClosed s) (hy : 0 ‚â§ y) :
    UpperSemicontinuousWithinAt (indicator s fun _x => y) t x :=
  (hs.upperSemicontinuous_indicator hy).upperSemicontinuousWithinAt t x

end

/-! #### Relationship with continuity -/


theorem upperSemicontinuous_iff_isOpen_preimage :
    UpperSemicontinuous f ‚Üî ‚àÄ y, IsOpen (f ‚Åª¬π' Iio y) :=
  ‚ü®fun H y => isOpen_iff_mem_nhds.2 fun x hx => H x y hx, fun H _x y y_lt =>
    IsOpen.mem_nhds (H y) y_lt‚ü©

theorem UpperSemicontinuous.isOpen_preimage (hf : UpperSemicontinuous f) (y : Œ≤) :
    IsOpen (f ‚Åª¬π' Iio y) :=
  upperSemicontinuous_iff_isOpen_preimage.1 hf y

section

variable {Œ≥ : Type*} [LinearOrder Œ≥]

theorem upperSemicontinuous_iff_isClosed_preimage {f : Œ± ‚Üí Œ≥} :
    UpperSemicontinuous f ‚Üî ‚àÄ y, IsClosed (f ‚Åª¬π' Ici y) := by
  rw [upperSemicontinuous_iff_isOpen_preimage]
  simp only [‚Üê isOpen_compl_iff, ‚Üê preimage_compl, compl_Ici]

theorem UpperSemicontinuous.isClosed_preimage {f : Œ± ‚Üí Œ≥} (hf : UpperSemicontinuous f) (y : Œ≥) :
    IsClosed (f ‚Åª¬π' Ici y) :=
  upperSemicontinuous_iff_isClosed_preimage.1 hf y

variable [TopologicalSpace Œ≥] [OrderTopology Œ≥]

theorem ContinuousWithinAt.upperSemicontinuousWithinAt {f : Œ± ‚Üí Œ≥} (h : ContinuousWithinAt f s x) :
    UpperSemicontinuousWithinAt f s x := fun _y hy => h (Iio_mem_nhds hy)

theorem ContinuousAt.upperSemicontinuousAt {f : Œ± ‚Üí Œ≥} (h : ContinuousAt f x) :
    UpperSemicontinuousAt f x := fun _y hy => h (Iio_mem_nhds hy)

theorem ContinuousOn.upperSemicontinuousOn {f : Œ± ‚Üí Œ≥} (h : ContinuousOn f s) :
    UpperSemicontinuousOn f s := fun x hx => (h x hx).upperSemicontinuousWithinAt

theorem Continuous.upperSemicontinuous {f : Œ± ‚Üí Œ≥} (h : Continuous f) : UpperSemicontinuous f :=
  fun _x => h.continuousAt.upperSemicontinuousAt

end

/-! #### Equivalent definitions -/

section

variable {Œ≥ : Type*} [CompleteLinearOrder Œ≥]

theorem upperSemicontinuousWithinAt_iff_limsup_le {f : Œ± ‚Üí Œ≥} :
    UpperSemicontinuousWithinAt f s x ‚Üî limsup f (ùìù[s] x) ‚â§ f x :=
  lowerSemicontinuousWithinAt_iff_le_liminf (Œ≥ := Œ≥·µí·µà)

alias ‚ü®UpperSemicontinuousWithinAt.limsup_le, _‚ü© := upperSemicontinuousWithinAt_iff_limsup_le

theorem upperSemicontinuousAt_iff_limsup_le {f : Œ± ‚Üí Œ≥} :
    UpperSemicontinuousAt f x ‚Üî limsup f (ùìù x) ‚â§ f x :=
  lowerSemicontinuousAt_iff_le_liminf (Œ≥ := Œ≥·µí·µà)

alias ‚ü®UpperSemicontinuousAt.limsup_le, _‚ü© := upperSemicontinuousAt_iff_limsup_le

theorem upperSemicontinuous_iff_limsup_le {f : Œ± ‚Üí Œ≥} :
    UpperSemicontinuous f ‚Üî ‚àÄ x, limsup f (ùìù x) ‚â§ f x :=
  lowerSemicontinuous_iff_le_liminf (Œ≥ := Œ≥·µí·µà)

alias ‚ü®UpperSemicontinuous.limsup_le, _‚ü© := upperSemicontinuous_iff_limsup_le

theorem upperSemicontinuousOn_iff_limsup_le {f : Œ± ‚Üí Œ≥} :
    UpperSemicontinuousOn f s ‚Üî ‚àÄ x ‚àà s, limsup f (ùìù[s] x) ‚â§ f x :=
  lowerSemicontinuousOn_iff_le_liminf (Œ≥ := Œ≥·µí·µà)

alias ‚ü®UpperSemicontinuousOn.limsup_le, _‚ü© := upperSemicontinuousOn_iff_limsup_le

end

section

variable {Œ≥ : Type*} [LinearOrder Œ≥] [TopologicalSpace Œ≥] [ClosedIicTopology Œ≥]

theorem upperSemicontinuousOn_iff_isClosed_hypograph {f : Œ± ‚Üí Œ≥} (hs : IsClosed s) :
    UpperSemicontinuousOn f s ‚Üî IsClosed {p : Œ± √ó Œ≥ | p.1 ‚àà s ‚àß p.2 ‚â§ f p.1} :=
  lowerSemicontinuousOn_iff_isClosed_epigraph hs (Œ≥ := Œ≥·µí·µà)

theorem upperSemicontinuous_iff_IsClosed_hypograph {f : Œ± ‚Üí Œ≥} :
    UpperSemicontinuous f ‚Üî IsClosed {p : Œ± √ó Œ≥ | p.2 ‚â§ f p.1} :=
  lowerSemicontinuous_iff_isClosed_epigraph (Œ≥ := Œ≥·µí·µà)

alias ‚ü®UpperSemicontinuous.IsClosed_hypograph, _‚ü© := upperSemicontinuous_iff_IsClosed_hypograph

end

/-! ### Composition -/

section

variable {Œ± : Type*} [TopologicalSpace Œ±]
variable {Œ≤ : Type*} [LinearOrder Œ≤]
variable {Œ≥ : Type*} [TopologicalSpace Œ≥]
variable {f : Œ± ‚Üí Œ≤} {g : Œ≥ ‚Üí Œ±} {s : Set Œ±} {a : Œ±} {c : Œ≥} {t : Set Œ≥}

theorem UpperSemicontinuousWithinAt.comp
    (hf : UpperSemicontinuousWithinAt f s (g c)) (hg : ContinuousWithinAt g t c)
    (hg' : MapsTo g t s) :
    UpperSemicontinuousWithinAt (f ‚àò g) t c :=
  LowerSemicontinuousWithinAt.comp (Œ≤ := Œ≤·µí·µà) hf hg hg'

theorem UpperSemicontinuousAt.comp
    (hf : UpperSemicontinuousAt f (g c)) (hg : ContinuousAt g c) :
    UpperSemicontinuousAt (f ‚àò g) c :=
  LowerSemicontinuousAt.comp (Œ≤ := Œ≤·µí·µà) hf hg

theorem UpperSemicontinuousOn.comp
    (hf : UpperSemicontinuousOn f s) (hg : ContinuousOn g t) (hg' : MapsTo g t s) :
    UpperSemicontinuousOn (f ‚àò g) t :=
  LowerSemicontinuousOn.comp (Œ≤ := Œ≤·µí·µà) hf hg hg'

theorem UpperSemicontinuous.comp
    (hf : UpperSemicontinuous f) (hg : Continuous g) : UpperSemicontinuous (f ‚àò g) :=
  LowerSemicontinuous.comp (Œ≤ := Œ≤·µí·µà) hf hg

@[simp] theorem upperSemicontinuousOn_iff_restrict {s : Set Œ±} :
    UpperSemicontinuous (s.restrict f) ‚Üî UpperSemicontinuousOn f s :=
  lowerSemicontinuous_restrict_iff (Œ≤ := Œ≤·µí·µà)

theorem upperSemicontinuousOn_iff_preimage_Iio :
    UpperSemicontinuousOn f s ‚Üî ‚àÄ b, ‚àÉ u : Set Œ±, IsOpen u ‚àß s ‚à© f ‚Åª¬π' Set.Iio b = s ‚à© u :=
  lowerSemicontinuousOn_iff_preimage_Ioi (Œ≤ := Œ≤·µí·µà)

theorem upperSemicontinuousOn_iff_preimage_Ici :
    UpperSemicontinuousOn f s ‚Üî ‚àÄ b, ‚àÉ v : Set Œ±, IsClosed v ‚àß s ‚à© f ‚Åª¬π' Set.Ici b = s ‚à© v :=
  lowerSemicontinuousOn_iff_preimage_Iic (Œ≥ := Œ≤·µí·µà)

end

section

variable {Œ≥ : Type*} [LinearOrder Œ≥] [TopologicalSpace Œ≥] [OrderTopology Œ≥]
variable {Œ¥ : Type*} [LinearOrder Œ¥] [TopologicalSpace Œ¥] [OrderTopology Œ¥]
variable {Œπ : Type*} [TopologicalSpace Œπ]

theorem ContinuousAt.comp_upperSemicontinuousWithinAt {g : Œ≥ ‚Üí Œ¥} {f : Œ± ‚Üí Œ≥}
    (hg : ContinuousAt g (f x)) (hf : UpperSemicontinuousWithinAt f s x) (gmon : Monotone g) :
    UpperSemicontinuousWithinAt (g ‚àò f) s x :=
  @ContinuousAt.comp_lowerSemicontinuousWithinAt Œ± _ x s Œ≥·µí·µà _ _ _ Œ¥·µí·µà _ _ _ g f hg hf gmon.dual

theorem ContinuousAt.comp_upperSemicontinuousAt {g : Œ≥ ‚Üí Œ¥} {f : Œ± ‚Üí Œ≥} (hg : ContinuousAt g (f x))
    (hf : UpperSemicontinuousAt f x) (gmon : Monotone g) : UpperSemicontinuousAt (g ‚àò f) x :=
  @ContinuousAt.comp_lowerSemicontinuousAt Œ± _ x Œ≥·µí·µà _ _ _ Œ¥·µí·µà _ _ _ g f hg hf gmon.dual

theorem Continuous.comp_upperSemicontinuousOn {g : Œ≥ ‚Üí Œ¥} {f : Œ± ‚Üí Œ≥} (hg : Continuous g)
    (hf : UpperSemicontinuousOn f s) (gmon : Monotone g) : UpperSemicontinuousOn (g ‚àò f) s :=
  fun x hx => hg.continuousAt.comp_upperSemicontinuousWithinAt (hf x hx) gmon

theorem Continuous.comp_upperSemicontinuous {g : Œ≥ ‚Üí Œ¥} {f : Œ± ‚Üí Œ≥} (hg : Continuous g)
    (hf : UpperSemicontinuous f) (gmon : Monotone g) : UpperSemicontinuous (g ‚àò f) := fun x =>
  hg.continuousAt.comp_upperSemicontinuousAt (hf x) gmon

theorem ContinuousAt.comp_upperSemicontinuousWithinAt_antitone {g : Œ≥ ‚Üí Œ¥} {f : Œ± ‚Üí Œ≥}
    (hg : ContinuousAt g (f x)) (hf : UpperSemicontinuousWithinAt f s x) (gmon : Antitone g) :
    LowerSemicontinuousWithinAt (g ‚àò f) s x :=
  @ContinuousAt.comp_upperSemicontinuousWithinAt Œ± _ x s Œ≥ _ _ _ Œ¥·µí·µà _ _ _ g f hg hf gmon

theorem ContinuousAt.comp_upperSemicontinuousAt_antitone {g : Œ≥ ‚Üí Œ¥} {f : Œ± ‚Üí Œ≥}
    (hg : ContinuousAt g (f x)) (hf : UpperSemicontinuousAt f x) (gmon : Antitone g) :
    LowerSemicontinuousAt (g ‚àò f) x :=
  @ContinuousAt.comp_upperSemicontinuousAt Œ± _ x Œ≥ _ _ _ Œ¥·µí·µà _ _ _ g f hg hf gmon

theorem Continuous.comp_upperSemicontinuousOn_antitone {g : Œ≥ ‚Üí Œ¥} {f : Œ± ‚Üí Œ≥} (hg : Continuous g)
    (hf : UpperSemicontinuousOn f s) (gmon : Antitone g) : LowerSemicontinuousOn (g ‚àò f) s :=
  fun x hx => hg.continuousAt.comp_upperSemicontinuousWithinAt_antitone (hf x hx) gmon

theorem Continuous.comp_upperSemicontinuous_antitone {g : Œ≥ ‚Üí Œ¥} {f : Œ± ‚Üí Œ≥} (hg : Continuous g)
    (hf : UpperSemicontinuous f) (gmon : Antitone g) : LowerSemicontinuous (g ‚àò f) := fun x =>
  hg.continuousAt.comp_upperSemicontinuousAt_antitone (hf x) gmon

theorem UpperSemicontinuousAt.comp_continuousAt {f : Œ± ‚Üí Œ≤} {g : Œπ ‚Üí Œ±} {x : Œπ}
    (hf : UpperSemicontinuousAt f (g x)) (hg : ContinuousAt g x) :
    UpperSemicontinuousAt (fun x ‚Ü¶ f (g x)) x :=
  fun _ lt ‚Ü¶ hg.eventually (hf _ lt)

theorem UpperSemicontinuousAt.comp_continuousAt_of_eq {f : Œ± ‚Üí Œ≤} {g : Œπ ‚Üí Œ±} {y : Œ±} {x : Œπ}
    (hf : UpperSemicontinuousAt f y) (hg : ContinuousAt g x) (hy : g x = y) :
    UpperSemicontinuousAt (fun x ‚Ü¶ f (g x)) x := by
  rw [‚Üê hy] at hf
  exact comp_continuousAt hf hg

theorem UpperSemicontinuous.comp_continuous {f : Œ± ‚Üí Œ≤} {g : Œπ ‚Üí Œ±}
    (hf : UpperSemicontinuous f) (hg : Continuous g) : UpperSemicontinuous fun x ‚Ü¶ f (g x) :=
  fun x ‚Ü¶ (hf (g x)).comp_continuousAt hg.continuousAt

end

/-! #### Addition -/


section

variable {Œπ : Type*} {Œ≥ : Type*} [AddCommMonoid Œ≥] [LinearOrder Œ≥] [IsOrderedAddMonoid Œ≥]
  [TopologicalSpace Œ≥] [OrderTopology Œ≥]

/-- The sum of two upper semicontinuous functions is upper semicontinuous. Formulated with an
explicit continuity assumption on addition, for application to `EReal`. The unprimed version of
the lemma uses `[ContinuousAdd]`. -/
theorem UpperSemicontinuousWithinAt.add' {f g : Œ± ‚Üí Œ≥} (hf : UpperSemicontinuousWithinAt f s x)
    (hg : UpperSemicontinuousWithinAt g s x)
    (hcont : ContinuousAt (fun p : Œ≥ √ó Œ≥ => p.1 + p.2) (f x, g x)) :
    UpperSemicontinuousWithinAt (fun z => f z + g z) s x :=
  LowerSemicontinuousWithinAt.add' (Œ≥ := Œ≥·µí·µà) hf hg hcont

/-- The sum of two upper semicontinuous functions is upper semicontinuous. Formulated with an
explicit continuity assumption on addition, for application to `EReal`. The unprimed version of
the lemma uses `[ContinuousAdd]`. -/
theorem UpperSemicontinuousAt.add' {f g : Œ± ‚Üí Œ≥} (hf : UpperSemicontinuousAt f x)
    (hg : UpperSemicontinuousAt g x)
    (hcont : ContinuousAt (fun p : Œ≥ √ó Œ≥ => p.1 + p.2) (f x, g x)) :
    UpperSemicontinuousAt (fun z => f z + g z) x := by
  simp_rw [‚Üê upperSemicontinuousWithinAt_univ_iff] at *
  exact hf.add' hg hcont

/-- The sum of two upper semicontinuous functions is upper semicontinuous. Formulated with an
explicit continuity assumption on addition, for application to `EReal`. The unprimed version of
the lemma uses `[ContinuousAdd]`. -/
theorem UpperSemicontinuousOn.add' {f g : Œ± ‚Üí Œ≥} (hf : UpperSemicontinuousOn f s)
    (hg : UpperSemicontinuousOn g s)
    (hcont : ‚àÄ x ‚àà s, ContinuousAt (fun p : Œ≥ √ó Œ≥ => p.1 + p.2) (f x, g x)) :
    UpperSemicontinuousOn (fun z => f z + g z) s := fun x hx =>
  (hf x hx).add' (hg x hx) (hcont x hx)

/-- The sum of two upper semicontinuous functions is upper semicontinuous. Formulated with an
explicit continuity assumption on addition, for application to `EReal`. The unprimed version of
the lemma uses `[ContinuousAdd]`. -/
theorem UpperSemicontinuous.add' {f g : Œ± ‚Üí Œ≥} (hf : UpperSemicontinuous f)
    (hg : UpperSemicontinuous g)
    (hcont : ‚àÄ x, ContinuousAt (fun p : Œ≥ √ó Œ≥ => p.1 + p.2) (f x, g x)) :
    UpperSemicontinuous fun z => f z + g z := fun x => (hf x).add' (hg x) (hcont x)

variable [ContinuousAdd Œ≥]

/-- The sum of two upper semicontinuous functions is upper semicontinuous. Formulated with
`[ContinuousAdd]`. The primed version of the lemma uses an explicit continuity assumption on
addition, for application to `EReal`. -/
theorem UpperSemicontinuousWithinAt.add {f g : Œ± ‚Üí Œ≥} (hf : UpperSemicontinuousWithinAt f s x)
    (hg : UpperSemicontinuousWithinAt g s x) :
    UpperSemicontinuousWithinAt (fun z => f z + g z) s x :=
  hf.add' hg continuous_add.continuousAt

/-- The sum of two upper semicontinuous functions is upper semicontinuous. Formulated with
`[ContinuousAdd]`. The primed version of the lemma uses an explicit continuity assumption on
addition, for application to `EReal`. -/
theorem UpperSemicontinuousAt.add {f g : Œ± ‚Üí Œ≥} (hf : UpperSemicontinuousAt f x)
    (hg : UpperSemicontinuousAt g x) : UpperSemicontinuousAt (fun z => f z + g z) x :=
  hf.add' hg continuous_add.continuousAt

/-- The sum of two upper semicontinuous functions is upper semicontinuous. Formulated with
`[ContinuousAdd]`. The primed version of the lemma uses an explicit continuity assumption on
addition, for application to `EReal`. -/
theorem UpperSemicontinuousOn.add {f g : Œ± ‚Üí Œ≥} (hf : UpperSemicontinuousOn f s)
    (hg : UpperSemicontinuousOn g s) : UpperSemicontinuousOn (fun z => f z + g z) s :=
  hf.add' hg fun _x _hx => continuous_add.continuousAt

/-- The sum of two upper semicontinuous functions is upper semicontinuous. Formulated with
`[ContinuousAdd]`. The primed version of the lemma uses an explicit continuity assumption on
addition, for application to `EReal`. -/
theorem UpperSemicontinuous.add {f g : Œ± ‚Üí Œ≥} (hf : UpperSemicontinuous f)
    (hg : UpperSemicontinuous g) : UpperSemicontinuous fun z => f z + g z :=
  hf.add' hg fun _x => continuous_add.continuousAt

theorem upperSemicontinuousWithinAt_sum {f : Œπ ‚Üí Œ± ‚Üí Œ≥} {a : Finset Œπ}
    (ha : ‚àÄ i ‚àà a, UpperSemicontinuousWithinAt (f i) s x) :
    UpperSemicontinuousWithinAt (fun z => ‚àë i ‚àà a, f i z) s x :=
  lowerSemicontinuousWithinAt_sum (Œ≥ := Œ≥·µí·µà) ha

theorem upperSemicontinuousAt_sum {f : Œπ ‚Üí Œ± ‚Üí Œ≥} {a : Finset Œπ}
    (ha : ‚àÄ i ‚àà a, UpperSemicontinuousAt (f i) x) :
    UpperSemicontinuousAt (fun z => ‚àë i ‚àà a, f i z) x := by
  simp_rw [‚Üê upperSemicontinuousWithinAt_univ_iff] at *
  exact upperSemicontinuousWithinAt_sum ha

theorem upperSemicontinuousOn_sum {f : Œπ ‚Üí Œ± ‚Üí Œ≥} {a : Finset Œπ}
    (ha : ‚àÄ i ‚àà a, UpperSemicontinuousOn (f i) s) :
    UpperSemicontinuousOn (fun z => ‚àë i ‚àà a, f i z) s := fun x hx =>
  upperSemicontinuousWithinAt_sum fun i hi => ha i hi x hx

theorem upperSemicontinuous_sum {f : Œπ ‚Üí Œ± ‚Üí Œ≥} {a : Finset Œπ}
    (ha : ‚àÄ i ‚àà a, UpperSemicontinuous (f i)) : UpperSemicontinuous fun z => ‚àë i ‚àà a, f i z :=
  fun x => upperSemicontinuousAt_sum fun i hi => ha i hi x

end

/-! #### Infimum -/

section

variable {Œ± : Type*} {Œ≤ : Type*} [TopologicalSpace Œ±] [LinearOrder Œ≤]
    {f g : Œ± ‚Üí Œ≤} {s : Set Œ±} {a : Œ±}

theorem UpperSemicontinuousWithinAt.inf
    (hf : UpperSemicontinuousWithinAt f s a) (hg : UpperSemicontinuousWithinAt g s a) :
    UpperSemicontinuousWithinAt (fun x ‚Ü¶ f x ‚äì g x) s a :=
  LowerSemicontinuousWithinAt.sup (Œ≤ := Œ≤·µí·µà) hf hg

theorem UpperSemicontinuousAt.inf
    (hf : UpperSemicontinuousAt f a) (hg : UpperSemicontinuousAt g a) :
    UpperSemicontinuousAt (fun x ‚Ü¶ f x ‚äì g x) a :=
  LowerSemicontinuousAt.sup (Œ≤ := Œ≤·µí·µà) hf hg

theorem UpperSemicontinuousOn.inf
    (hf : UpperSemicontinuousOn f s) (hg : UpperSemicontinuousOn g s) :
    UpperSemicontinuousOn (fun x ‚Ü¶ f x ‚äì g x) s :=
  LowerSemicontinuousOn.sup (Œ≤ := Œ≤·µí·µà) hf hg

theorem UpperSemicontinuous.inf (hf : UpperSemicontinuous f) (hg : UpperSemicontinuous g) :
    UpperSemicontinuous (fun x ‚Ü¶ f x ‚äì g x) :=
  LowerSemicontinuous.sup (Œ≤ := Œ≤·µí·µà) hf hg

theorem UpperSemicontinuousWithinAt.sup
    (hf : UpperSemicontinuousWithinAt f s a) (hg : UpperSemicontinuousWithinAt g s a) :
    UpperSemicontinuousWithinAt (fun x ‚Ü¶ f x ‚äî g x) s a :=
  LowerSemicontinuousWithinAt.inf (Œ≤ := Œ≤·µí·µà) hf hg

theorem UpperSemicontinuousAt.sup
    (hf : UpperSemicontinuousAt f a) (hg : UpperSemicontinuousAt g a) :
    UpperSemicontinuousAt (fun x ‚Ü¶ f x ‚äî g x) a :=
  LowerSemicontinuousAt.inf (Œ≤ := Œ≤·µí·µà) hf hg

theorem UpperSemicontinuousOn.sup
    (hf : UpperSemicontinuousOn f s) (hg : UpperSemicontinuousOn g s) :
    UpperSemicontinuousOn (fun x ‚Ü¶ f x ‚äî g x) s :=
  LowerSemicontinuousOn.inf (Œ≤ := Œ≤·µí·µà) hf hg

theorem UpperSemicontinuous.sup (hf : UpperSemicontinuous f) (hg : UpperSemicontinuous g) :
    UpperSemicontinuous fun x ‚Ü¶ f x ‚äî g x :=
  LowerSemicontinuous.inf (Œ≤ := Œ≤·µí·µà) hf hg


end

section

variable {Œπ : Sort*} {Œ¥ Œ¥' : Type*} [CompleteLinearOrder Œ¥] [ConditionallyCompleteLinearOrder Œ¥']

theorem upperSemicontinuousWithinAt_ciInf {f : Œπ ‚Üí Œ± ‚Üí Œ¥'}
    (bdd : ‚àÄ·∂† y in ùìù[s] x, BddBelow (range fun i => f i y))
    (h : ‚àÄ i, UpperSemicontinuousWithinAt (f i) s x) :
    UpperSemicontinuousWithinAt (fun x' => ‚®Ö i, f i x') s x :=
  @lowerSemicontinuousWithinAt_ciSup Œ± _ x s Œπ Œ¥'·µí·µà _ f bdd h

theorem upperSemicontinuousWithinAt_iInf {f : Œπ ‚Üí Œ± ‚Üí Œ¥}
    (h : ‚àÄ i, UpperSemicontinuousWithinAt (f i) s x) :
    UpperSemicontinuousWithinAt (fun x' => ‚®Ö i, f i x') s x :=
  @lowerSemicontinuousWithinAt_iSup Œ± _ x s Œπ Œ¥·µí·µà _ f h

theorem upperSemicontinuousWithinAt_biInf {p : Œπ ‚Üí Prop} {f : ‚àÄ i, p i ‚Üí Œ± ‚Üí Œ¥}
    (h : ‚àÄ i hi, UpperSemicontinuousWithinAt (f i hi) s x) :
    UpperSemicontinuousWithinAt (fun x' => ‚®Ö (i) (hi), f i hi x') s x :=
  upperSemicontinuousWithinAt_iInf fun i => upperSemicontinuousWithinAt_iInf fun hi => h i hi

theorem upperSemicontinuousAt_ciInf {f : Œπ ‚Üí Œ± ‚Üí Œ¥'}
    (bdd : ‚àÄ·∂† y in ùìù x, BddBelow (range fun i => f i y)) (h : ‚àÄ i, UpperSemicontinuousAt (f i) x) :
    UpperSemicontinuousAt (fun x' => ‚®Ö i, f i x') x :=
  @lowerSemicontinuousAt_ciSup Œ± _ x Œπ Œ¥'·µí·µà _ f bdd h

theorem upperSemicontinuousAt_iInf {f : Œπ ‚Üí Œ± ‚Üí Œ¥} (h : ‚àÄ i, UpperSemicontinuousAt (f i) x) :
    UpperSemicontinuousAt (fun x' => ‚®Ö i, f i x') x :=
  @lowerSemicontinuousAt_iSup Œ± _ x Œπ Œ¥·µí·µà _ f h

theorem upperSemicontinuousAt_biInf {p : Œπ ‚Üí Prop} {f : ‚àÄ i, p i ‚Üí Œ± ‚Üí Œ¥}
    (h : ‚àÄ i hi, UpperSemicontinuousAt (f i hi) x) :
    UpperSemicontinuousAt (fun x' => ‚®Ö (i) (hi), f i hi x') x :=
  upperSemicontinuousAt_iInf fun i => upperSemicontinuousAt_iInf fun hi => h i hi

theorem upperSemicontinuousOn_ciInf {f : Œπ ‚Üí Œ± ‚Üí Œ¥'}
    (bdd : ‚àÄ x ‚àà s, BddBelow (range fun i => f i x)) (h : ‚àÄ i, UpperSemicontinuousOn (f i) s) :
    UpperSemicontinuousOn (fun x' => ‚®Ö i, f i x') s := fun x hx =>
  upperSemicontinuousWithinAt_ciInf (eventually_nhdsWithin_of_forall bdd) fun i => h i x hx

theorem upperSemicontinuousOn_iInf {f : Œπ ‚Üí Œ± ‚Üí Œ¥} (h : ‚àÄ i, UpperSemicontinuousOn (f i) s) :
    UpperSemicontinuousOn (fun x' => ‚®Ö i, f i x') s := fun x hx =>
  upperSemicontinuousWithinAt_iInf fun i => h i x hx

theorem upperSemicontinuousOn_biInf {p : Œπ ‚Üí Prop} {f : ‚àÄ i, p i ‚Üí Œ± ‚Üí Œ¥}
    (h : ‚àÄ i hi, UpperSemicontinuousOn (f i hi) s) :
    UpperSemicontinuousOn (fun x' => ‚®Ö (i) (hi), f i hi x') s :=
  upperSemicontinuousOn_iInf fun i => upperSemicontinuousOn_iInf fun hi => h i hi

theorem upperSemicontinuous_ciInf {f : Œπ ‚Üí Œ± ‚Üí Œ¥'} (bdd : ‚àÄ x, BddBelow (range fun i => f i x))
    (h : ‚àÄ i, UpperSemicontinuous (f i)) : UpperSemicontinuous fun x' => ‚®Ö i, f i x' := fun x =>
  upperSemicontinuousAt_ciInf (Eventually.of_forall bdd) fun i => h i x

theorem upperSemicontinuous_iInf {f : Œπ ‚Üí Œ± ‚Üí Œ¥} (h : ‚àÄ i, UpperSemicontinuous (f i)) :
    UpperSemicontinuous fun x' => ‚®Ö i, f i x' := fun x => upperSemicontinuousAt_iInf fun i => h i x

theorem upperSemicontinuous_biInf {p : Œπ ‚Üí Prop} {f : ‚àÄ i, p i ‚Üí Œ± ‚Üí Œ¥}
    (h : ‚àÄ i hi, UpperSemicontinuous (f i hi)) :
    UpperSemicontinuous fun x' => ‚®Ö (i) (hi), f i hi x' :=
  upperSemicontinuous_iInf fun i => upperSemicontinuous_iInf fun hi => h i hi

end

section

variable {Œ≥ : Type*} [LinearOrder Œ≥] [TopologicalSpace Œ≥] [OrderTopology Œ≥]

theorem continuousWithinAt_iff_lower_upperSemicontinuousWithinAt {f : Œ± ‚Üí Œ≥} :
    ContinuousWithinAt f s x ‚Üî
      LowerSemicontinuousWithinAt f s x ‚àß UpperSemicontinuousWithinAt f s x := by
  refine ‚ü®fun h => ‚ü®h.lowerSemicontinuousWithinAt, h.upperSemicontinuousWithinAt‚ü©, ?_‚ü©
  rintro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©
  intro v hv
  simp only [Filter.mem_map]
  by_cases Hl : ‚àÉ l, l < f x
  ¬∑ rcases exists_Ioc_subset_of_mem_nhds hv Hl with ‚ü®l, lfx, hl‚ü©
    by_cases Hu : ‚àÉ u, f x < u
    ¬∑ rcases exists_Ico_subset_of_mem_nhds hv Hu with ‚ü®u, fxu, hu‚ü©
      filter_upwards [h‚ÇÅ l lfx, h‚ÇÇ u fxu] with a lfa fau
      rcases le_or_gt (f a) (f x) with h | h
      ¬∑ exact hl ‚ü®lfa, h‚ü©
      ¬∑ exact hu ‚ü®le_of_lt h, fau‚ü©
    ¬∑ simp only [not_exists, not_lt] at Hu
      filter_upwards [h‚ÇÅ l lfx] with a lfa using hl ‚ü®lfa, Hu (f a)‚ü©
  ¬∑ simp only [not_exists, not_lt] at Hl
    by_cases Hu : ‚àÉ u, f x < u
    ¬∑ rcases exists_Ico_subset_of_mem_nhds hv Hu with ‚ü®u, fxu, hu‚ü©
      filter_upwards [h‚ÇÇ u fxu] with a lfa
      apply hu
      exact ‚ü®Hl (f a), lfa‚ü©
    ¬∑ simp only [not_exists, not_lt] at Hu
      apply Filter.Eventually.of_forall
      intro a
      have : f a = f x := le_antisymm (Hu _) (Hl _)
      rw [this]
      exact mem_of_mem_nhds hv

theorem continuousAt_iff_lower_upperSemicontinuousAt {f : Œ± ‚Üí Œ≥} :
    ContinuousAt f x ‚Üî LowerSemicontinuousAt f x ‚àß UpperSemicontinuousAt f x := by
  simp_rw [‚Üê continuousWithinAt_univ, ‚Üê lowerSemicontinuousWithinAt_univ_iff, ‚Üê
    upperSemicontinuousWithinAt_univ_iff, continuousWithinAt_iff_lower_upperSemicontinuousWithinAt]

theorem continuousOn_iff_lower_upperSemicontinuousOn {f : Œ± ‚Üí Œ≥} :
    ContinuousOn f s ‚Üî LowerSemicontinuousOn f s ‚àß UpperSemicontinuousOn f s := by
  simp only [ContinuousOn, continuousWithinAt_iff_lower_upperSemicontinuousWithinAt]
  exact
    ‚ü®fun H => ‚ü®fun x hx => (H x hx).1, fun x hx => (H x hx).2‚ü©, fun H x hx => ‚ü®H.1 x hx, H.2 x hx‚ü©‚ü©

theorem continuous_iff_lower_upperSemicontinuous {f : Œ± ‚Üí Œ≥} :
    Continuous f ‚Üî LowerSemicontinuous f ‚àß UpperSemicontinuous f := by
  simp_rw [‚Üê continuousOn_univ, continuousOn_iff_lower_upperSemicontinuousOn,
    lowerSemicontinuousOn_univ_iff, upperSemicontinuousOn_univ_iff]

end
