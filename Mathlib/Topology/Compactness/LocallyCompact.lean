/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro, Yury Kudryashov
-/
import Mathlib.Topology.Compactness.Compact
/-!
# Locally compact spaces

This file contains basic results about locally compact spaces.
-/

open Set Filter Topology TopologicalSpace

variable {X : Type*} {Y : Type*} {Œπ : Type*}
variable [TopologicalSpace X] [TopologicalSpace Y] {s t : Set X}

instance [WeaklyLocallyCompactSpace X] [WeaklyLocallyCompactSpace Y] :
    WeaklyLocallyCompactSpace (X √ó Y) where
  exists_compact_mem_nhds x :=
    let ‚ü®s‚ÇÅ, hc‚ÇÅ, h‚ÇÅ‚ü© := exists_compact_mem_nhds x.1
    let ‚ü®s‚ÇÇ, hc‚ÇÇ, h‚ÇÇ‚ü© := exists_compact_mem_nhds x.2
    ‚ü®s‚ÇÅ √óÀ¢ s‚ÇÇ, hc‚ÇÅ.prod hc‚ÇÇ, prod_mem_nhds h‚ÇÅ h‚ÇÇ‚ü©

instance {Œπ : Type*} [Finite Œπ] {X : Œπ ‚Üí Type*} [(i : Œπ) ‚Üí TopologicalSpace (X i)]
    [(i : Œπ) ‚Üí WeaklyLocallyCompactSpace (X i)] :
    WeaklyLocallyCompactSpace ((i : Œπ) ‚Üí X i) where
  exists_compact_mem_nhds f := by
    choose s hsc hs using fun i ‚Ü¶ exists_compact_mem_nhds (f i)
    exact ‚ü®pi univ s, isCompact_univ_pi hsc, set_pi_mem_nhds univ.toFinite fun i _ ‚Ü¶ hs i‚ü©

instance (priority := 100) [CompactSpace X] : WeaklyLocallyCompactSpace X where
  exists_compact_mem_nhds _ := ‚ü®univ, isCompact_univ, univ_mem‚ü©

protected theorem Topology.IsClosedEmbedding.weaklyLocallyCompactSpace [WeaklyLocallyCompactSpace Y]
    {f : X ‚Üí Y} (hf : IsClosedEmbedding f) : WeaklyLocallyCompactSpace X where
  exists_compact_mem_nhds x :=
    let ‚ü®K, hK, hKx‚ü© := exists_compact_mem_nhds (f x)
    ‚ü®f ‚Åª¬π' K, hf.isCompact_preimage hK, hf.continuous.continuousAt hKx‚ü©

protected theorem IsClosed.weaklyLocallyCompactSpace [WeaklyLocallyCompactSpace X]
    {s : Set X} (hs : IsClosed s) : WeaklyLocallyCompactSpace s :=
  hs.isClosedEmbedding_subtypeVal.weaklyLocallyCompactSpace

theorem IsOpenQuotientMap.weaklyLocallyCompactSpace [WeaklyLocallyCompactSpace X]
    {f : X ‚Üí Y} (hf : IsOpenQuotientMap f) : WeaklyLocallyCompactSpace Y where
  exists_compact_mem_nhds := by
    refine hf.surjective.forall.2 fun x ‚Ü¶ ?_
    rcases exists_compact_mem_nhds x with ‚ü®K, hKc, hKx‚ü©
    exact ‚ü®f '' K, hKc.image hf.continuous, hf.isOpenMap.image_mem_nhds hKx‚ü©

/-- In a weakly locally compact space,
every compact set is contained in the interior of a compact set. -/
theorem exists_compact_superset [WeaklyLocallyCompactSpace X] {K : Set X} (hK : IsCompact K) :
    ‚àÉ K', IsCompact K' ‚àß K ‚äÜ interior K' := by
  choose s hc hmem using fun x : X ‚Ü¶ exists_compact_mem_nhds x
  rcases hK.elim_nhds_subcover _ fun x _ ‚Ü¶ interior_mem_nhds.2 (hmem x) with ‚ü®I, -, hIK‚ü©
  refine ‚ü®‚ãÉ x ‚àà I, s x, I.isCompact_biUnion fun _ _ ‚Ü¶ hc _, hIK.trans ?_‚ü©
  exact iUnion‚ÇÇ_subset fun x hx ‚Ü¶ interior_mono <| subset_iUnion‚ÇÇ (s := fun x _ ‚Ü¶ s x) x hx

/-- In a weakly locally compact space,
the filters `ùìù x` and `cocompact X` are disjoint for all `X`. -/
theorem disjoint_nhds_cocompact [WeaklyLocallyCompactSpace X] (x : X) :
    Disjoint (ùìù x) (cocompact X) :=
  let ‚ü®_, hc, hx‚ü© := exists_compact_mem_nhds x
  disjoint_of_disjoint_of_mem disjoint_compl_right hx hc.compl_mem_cocompact

theorem compact_basis_nhds [LocallyCompactSpace X] (x : X) :
    (ùìù x).HasBasis (fun s => s ‚àà ùìù x ‚àß IsCompact s) fun s => s :=
  hasBasis_self.2 <| by simpa only [and_comm] using LocallyCompactSpace.local_compact_nhds x

theorem local_compact_nhds [LocallyCompactSpace X] {x : X} {n : Set X} (h : n ‚àà ùìù x) :
    ‚àÉ s ‚àà ùìù x, s ‚äÜ n ‚àß IsCompact s :=
  LocallyCompactSpace.local_compact_nhds _ _ h

theorem LocallyCompactSpace.of_hasBasis {Œπ : X ‚Üí Type*} {p : ‚àÄ x, Œπ x ‚Üí Prop}
    {s : ‚àÄ x, Œπ x ‚Üí Set X} (h : ‚àÄ x, (ùìù x).HasBasis (p x) (s x))
    (hc : ‚àÄ x i, p x i ‚Üí IsCompact (s x i)) : LocallyCompactSpace X :=
  ‚ü®fun x _t ht =>
    let ‚ü®i, hp, ht‚ü© := (h x).mem_iff.1 ht
    ‚ü®s x i, (h x).mem_of_mem hp, ht, hc x i hp‚ü©‚ü©

instance Prod.locallyCompactSpace (X : Type*) (Y : Type*) [TopologicalSpace X]
    [TopologicalSpace Y] [LocallyCompactSpace X] [LocallyCompactSpace Y] :
    LocallyCompactSpace (X √ó Y) :=
  have := fun x : X √ó Y => (compact_basis_nhds x.1).prod_nhds' (compact_basis_nhds x.2)
  .of_hasBasis this fun _ _ ‚ü®‚ü®_, h‚ÇÅ‚ü©, _, h‚ÇÇ‚ü© => h‚ÇÅ.prod h‚ÇÇ

section Pi

variable {X : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (X i)] [‚àÄ i, LocallyCompactSpace (X i)]

/-- In general it suffices that all but finitely many of the spaces are compact,
  but that's not straightforward to state and use. -/
instance Pi.locallyCompactSpace_of_finite [Finite Œπ] : LocallyCompactSpace (‚àÄ i, X i) :=
  ‚ü®fun t n hn => by
    rw [nhds_pi, Filter.mem_pi] at hn
    obtain ‚ü®s, -, n', hn', hsub‚ü© := hn
    choose n'' hn'' hsub' hc using fun i =>
      LocallyCompactSpace.local_compact_nhds (t i) (n' i) (hn' i)
    refine ‚ü®(Set.univ : Set Œπ).pi n'', ?_, subset_trans (fun _ h => ?_) hsub, isCompact_univ_pi hc‚ü©
    ¬∑ exact (set_pi_mem_nhds_iff (@Set.finite_univ Œπ _) _).mpr fun i _ => hn'' i
    ¬∑ exact fun i _ => hsub' i (h i trivial)‚ü©

/-- For spaces that are not Hausdorff. -/
instance Pi.locallyCompactSpace [‚àÄ i, CompactSpace (X i)] : LocallyCompactSpace (‚àÄ i, X i) :=
  ‚ü®fun t n hn => by
    rw [nhds_pi, Filter.mem_pi] at hn
    obtain ‚ü®s, hs, n', hn', hsub‚ü© := hn
    choose n'' hn'' hsub' hc using fun i =>
      LocallyCompactSpace.local_compact_nhds (t i) (n' i) (hn' i)
    refine ‚ü®s.pi n'', ?_, subset_trans (fun _ => ?_) hsub, ?_‚ü©
    ¬∑ exact (set_pi_mem_nhds_iff hs _).mpr fun i _ => hn'' i
    ¬∑ exact forall‚ÇÇ_imp fun i _ hi' => hsub' i hi'
    ¬∑ classical
      rw [‚Üê Set.univ_pi_ite]
      refine isCompact_univ_pi fun i => ?_
      by_cases h : i ‚àà s
      ¬∑ rw [if_pos h]
        exact hc i
      ¬∑ rw [if_neg h]
        exact CompactSpace.isCompact_univ‚ü©

instance Function.locallyCompactSpace_of_finite [Finite Œπ] [LocallyCompactSpace Y] :
    LocallyCompactSpace (Œπ ‚Üí Y) :=
  Pi.locallyCompactSpace_of_finite

instance Function.locallyCompactSpace [LocallyCompactSpace Y] [CompactSpace Y] :
    LocallyCompactSpace (Œπ ‚Üí Y) :=
  Pi.locallyCompactSpace

end Pi

instance (priority := 900) [LocallyCompactSpace X] : LocallyCompactPair X Y where
  exists_mem_nhds_isCompact_mapsTo hf hs :=
    let ‚ü®K, hKx, hKs, hKc‚ü© := local_compact_nhds (hf.continuousAt hs); ‚ü®K, hKx, hKc, hKs‚ü©

instance (priority := 100) [LocallyCompactSpace X] : WeaklyLocallyCompactSpace X where
  exists_compact_mem_nhds (x : X) :=
    let ‚ü®K, hx, _, hKc‚ü© := local_compact_nhds (x := x) univ_mem; ‚ü®K, hKc, hx‚ü©

/-- A reformulation of the definition of locally compact space: In a locally compact space,
  every open set containing `x` has a compact subset containing `x` in its interior. -/
theorem exists_compact_subset [LocallyCompactSpace X] {x : X} {U : Set X} (hU : IsOpen U)
    (hx : x ‚àà U) : ‚àÉ K : Set X, IsCompact K ‚àß x ‚àà interior K ‚àß K ‚äÜ U := by
  rcases LocallyCompactSpace.local_compact_nhds x U (hU.mem_nhds hx) with ‚ü®K, h1K, h2K, h3K‚ü©
  exact ‚ü®K, h3K, mem_interior_iff_mem_nhds.2 h1K, h2K‚ü©

/-- If `f : X ‚Üí Y` is a continuous map in a locally compact pair of topological spaces,
`K : set X` is a compact set, and `U` is an open neighbourhood of `f '' K`,
then there exists a compact neighbourhood `L` of `K` such that `f` maps `L` to `U`.

This is a generalization of `exists_mem_nhds_isCompact_mapsTo`. -/
lemma exists_mem_nhdsSet_isCompact_mapsTo [LocallyCompactPair X Y] {f : X ‚Üí Y} {K : Set X}
    {U : Set Y} (hf : Continuous f) (hK : IsCompact K) (hU : IsOpen U) (hKU : MapsTo f K U) :
    ‚àÉ L ‚àà ùìùÀ¢ K, IsCompact L ‚àß MapsTo f L U := by
  choose! V hxV hVc hVU using fun x (hx : x ‚àà K) ‚Ü¶
    exists_mem_nhds_isCompact_mapsTo hf (hU.mem_nhds (hKU hx))
  rcases hK.elim_nhds_subcover_nhdsSet hxV with ‚ü®s, hsK, hKs‚ü©
  exact ‚ü®_, hKs, s.isCompact_biUnion fun x hx ‚Ü¶ hVc x (hsK x hx), mapsTo_iUnion‚ÇÇ.2 fun x hx ‚Ü¶
    hVU x (hsK x hx)‚ü©

/-- In a locally compact space, for every containment `K ‚äÜ U` of a compact set `K` in an open
  set `U`, there is a compact neighborhood `L` such that `K ‚äÜ L ‚äÜ U`: equivalently, there is a
  compact `L` such that `K ‚äÜ interior L` and `L ‚äÜ U`.
  See also `exists_compact_closed_between`, in which one guarantees additionally that `L` is closed
  if the space is regular. -/
theorem exists_compact_between [LocallyCompactSpace X] {K U : Set X} (hK : IsCompact K)
    (hU : IsOpen U) (h_KU : K ‚äÜ U) : ‚àÉ L, IsCompact L ‚àß K ‚äÜ interior L ‚àß L ‚äÜ U :=
  let ‚ü®L, hKL, hL, hLU‚ü© := exists_mem_nhdsSet_isCompact_mapsTo continuous_id hK hU h_KU
  ‚ü®L, hL, subset_interior_iff_mem_nhdsSet.2 hKL, hLU‚ü©

theorem IsOpenQuotientMap.locallyCompactSpace [LocallyCompactSpace X] {f : X ‚Üí Y}
    (hf : IsOpenQuotientMap f) : LocallyCompactSpace Y where
  local_compact_nhds := by
    refine hf.surjective.forall.2 fun x U hU ‚Ü¶ ?_
    rcases local_compact_nhds (hf.continuous.continuousAt hU) with ‚ü®K, hKx, hKU, hKc‚ü©
    exact ‚ü®f '' K, hf.isOpenMap.image_mem_nhds hKx, image_subset_iff.2 hKU, hKc.image hf.continuous‚ü©

/-- If `f` is a topology inducing map with a locally compact codomain and a locally closed range,
then the domain of `f` is a locally compact space. -/
theorem Topology.IsInducing.locallyCompactSpace [LocallyCompactSpace Y] {f : X ‚Üí Y}
    (hf : IsInducing f) (h : IsLocallyClosed (range f)) : LocallyCompactSpace X := by
  rcases h with ‚ü®U, Z, hU, hZ, hUZ‚ü©
  have (x : X) : (ùìù x).HasBasis (fun s ‚Ü¶ (s ‚àà ùìù (f x) ‚àß IsCompact s) ‚àß s ‚äÜ U)
      (fun s ‚Ü¶ f ‚Åª¬π' (s ‚à© Z)) := by
    have H : U ‚àà ùìù (f x) := hU.mem_nhds (hUZ.subset <| mem_range_self _).1
    rw [hf.nhds_eq_comap, ‚Üê comap_nhdsWithin_range, hUZ,
      nhdsWithin_inter_of_mem (nhdsWithin_le_nhds H)]
    exact (nhdsWithin_hasBasis ((compact_basis_nhds (f x)).restrict_subset H) _).comap _
  refine .of_hasBasis this fun x s ‚ü®‚ü®_, hs‚ü©, hsU‚ü© ‚Ü¶ ?_
  rw [hf.isCompact_preimage_iff]
  exacts [hs.inter_right hZ, hUZ ‚ñ∏ by gcongr]

protected theorem Topology.IsClosedEmbedding.locallyCompactSpace [LocallyCompactSpace Y] {f : X ‚Üí Y}
    (hf : IsClosedEmbedding f) : LocallyCompactSpace X :=
  hf.isInducing.locallyCompactSpace hf.isClosed_range.isLocallyClosed

protected theorem Topology.IsOpenEmbedding.locallyCompactSpace [LocallyCompactSpace Y] {f : X ‚Üí Y}
    (hf : IsOpenEmbedding f) : LocallyCompactSpace X :=
  hf.isInducing.locallyCompactSpace hf.isOpen_range.isLocallyClosed

protected theorem IsLocallyClosed.locallyCompactSpace [LocallyCompactSpace X] {s : Set X}
    (hs : IsLocallyClosed s) : LocallyCompactSpace s :=
  IsEmbedding.subtypeVal.locallyCompactSpace <| by rwa [Subtype.range_val]

protected theorem IsClosed.locallyCompactSpace [LocallyCompactSpace X] {s : Set X}
    (hs : IsClosed s) : LocallyCompactSpace s :=
  hs.isLocallyClosed.locallyCompactSpace

protected theorem IsOpen.locallyCompactSpace [LocallyCompactSpace X] {s : Set X} (hs : IsOpen s) :
    LocallyCompactSpace s :=
  hs.isLocallyClosed.locallyCompactSpace
