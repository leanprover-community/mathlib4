/-
Copyright (c) 2023 Josha Dekker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Josha Dekker
-/
import Mathlib.Topology.Bases
import Mathlib.Order.Filter.CountableInter
import Mathlib.Topology.Compactness.Compact

/-!
# LindelÃ¶f sets and LindelÃ¶f spaces

## Main definitions

We define the following properties for sets in a topological space:

* `IsLindelof`: a set such that each open cover has a countable subcover. This is defined in Mathlib
  using filters.
* `LindelofSpace`: typeclass stating that the whole space is a LindÃ«lof set.
* `NonLindelofSpace`: a space that is not a LindÃ«lof space.

## Main results

* ToBeAdded
-/
open Set Filter Topology TopologicalSpace


universe u v

variable {X : Type u} {Y : Type v} {Î¹ : Type*}

variable [TopologicalSpace X] [TopologicalSpace Y] {s t : Set X}

section Lindelof

/-- A set `s` is LindelÃ¶f if every open cover has a countable subcover. This is implemented in
  Mathlib by showing that for every nontrivial filter `f` with the countable intersection
  property that contains `s`, there exists `a âˆˆ s` such that every set of `f`
  meets every neighborhood of `a`. The equivalence of these two still needs to be proven in Mathlib
  (Work in progress). -/
def IsLindelof (s : Set X) :=
  âˆ€ â¦ƒfâ¦„ [NeBot f] [CountableInterFilter f], f â‰¤ ğ“Ÿ s â†’ âˆƒ x âˆˆ s, ClusterPt x f

/-- Type class for LindelÃ¶f spaces.  -/
class LindelofSpace (X : Type*) [TopologicalSpace X] : Prop where
  /-- In a LindelÃ¶f space, `Set.univ` is a LindelÃ¶f set. -/
  isLindelof_univ : IsLindelof (univ : Set X)

/-- `X` is a non-LindelÃ¶f topological space if it is not a LindelÃ¶f space. -/
class NonLindelofSpace (X : Type*) [TopologicalSpace X] : Prop where
  /-- In a non-LindelÃ¶f space, `Set.univ` is not a LindelÃ¶f set. -/
  nonLindelof_univ : Â¬IsLindelof (univ : Set X)

/-- The complement to a LindelÃ¶f set belongs to a filter `f` with the countable intersection
  property if it belongs to each filter `ğ“ x âŠ“ f`, `x âˆˆ s`. -/
theorem IsLindelof.compl_mem_sets (hs : IsLindelof s) {f : Filter X} [CountableInterFilter f]
    (hf : âˆ€ x âˆˆ s, sá¶œ âˆˆ ğ“ x âŠ“ f) : sá¶œ âˆˆ f := by
  contrapose! hf
  simp only [not_mem_iff_inf_principal_compl, compl_compl, inf_assoc] at hf âŠ¢
  apply @hs
  apply inf_le_right

/-- The complement to a LindelÃ¶f set belongs to a filter `f` with the countable intersection
  property if each `x âˆˆ s` has a neighborhood `t` within `s` such that `tá¶œ` belongs to `f`. -/
theorem IsLindelof.compl_mem_sets_of_nhdsWithin (hs : IsLindelof s) {f : Filter X}
    [CountableInterFilter f] (hf : âˆ€ x âˆˆ s, âˆƒ t âˆˆ ğ“[s] x, tá¶œ âˆˆ f) : sá¶œ âˆˆ f := by
  refine' hs.compl_mem_sets fun x hx => _
  rcases hf x hx with âŸ¨t, ht, hstâŸ©
  replace ht := mem_inf_principal.1 ht
  apply mem_inf_of_inter ht hst
  rintro x âŸ¨hâ‚, hâ‚‚âŸ© hs
  exact hâ‚‚ (hâ‚ hs)

/-- If `p : Set X â†’ Prop` is stable under restriction and union, and each point `x`
  of a LindelÃ¶f set `s` has a neighborhood `t` within `s` such that `p t`, then `p s` holds. -/
@[elab_as_elim]
theorem IsLindelof.induction_on (hs : IsLindelof s) {p : Set X â†’ Prop} (he : p âˆ…)
    (hmono : âˆ€ â¦ƒs tâ¦„, s âŠ† t â†’ p t â†’ p s)
    (hcountable_union : âˆ€ (S : Set (Set X)), S.Countable â†’ (âˆ€ s âˆˆ S, p s) â†’ p (â‹ƒ s âˆˆ S, s))
    (hnhds : âˆ€ x âˆˆ s, âˆƒ t âˆˆ ğ“[s] x, p t) : p s := by
  let f : Filter X :=
    { sets := { t | p tá¶œ }
      univ_sets := by simpa
      sets_of_superset := fun htâ‚ ht => hmono (compl_subset_compl.2 ht) htâ‚
      inter_sets := by
        intro htâ‚ htâ‚‚
        simp [compl_inter]
        intro pâ‚ pâ‚‚
        let Se : Set (Set X) := {htâ‚á¶œ, htâ‚‚á¶œ}
        have hSe : Se.Countable := by simp
        have : âˆ€ s âˆˆ Se, p s := by
          intros x hx
          rcases hx with âŸ¨rfl|_âŸ©
          Â· exact pâ‚
          Â· have h : x = htâ‚‚á¶œ := by
              assumption
            rw [h]
            exact pâ‚‚
        have := hcountable_union Se hSe this
        have : â‹ƒ sâˆˆ Se, s = htâ‚á¶œ âˆª htâ‚‚á¶œ := by simp
        rwa [â† this]
        }
  have : CountableInterFilter f := by
    apply CountableInterFilter.mk
    simp [compl_sInter]
    intro S hS hsp
    let f := fun (x : Set X) â†¦ xá¶œ
    let S' := f '' S
    have hsp : âˆ€ s âˆˆ S', p s := by simpa
    have hS' : S'.Countable := Countable.image hS _
    have : â‹ƒ s âˆˆ S, sá¶œ = â‹ƒ s âˆˆ S', s := by simp
    rw [this]
    apply hcountable_union S' hS' hsp
  have : sá¶œ âˆˆ f := hs.compl_mem_sets_of_nhdsWithin (by simpa using hnhds)
  rwa [â† compl_compl s]

/-- The intersection of a LindelÃ¶f set and a closed set is a LindelÃ¶f set. -/
theorem IsLindelof.inter_right (hs : IsLindelof s) (ht : IsClosed t) : IsLindelof (s âˆ© t) := by
  intro f hnf _ hstf
  obtain âŸ¨x, hsx, hxâŸ© : âˆƒ x âˆˆ s, ClusterPt x f :=
    hs (le_trans hstf (le_principal_iff.2 (inter_subset_left _ _)))
  have : x âˆˆ t := ht.mem_of_nhdsWithin_neBot <|
    hx.mono <| le_trans hstf (le_principal_iff.2 (inter_subset_right _ _))
  exact âŸ¨x, âŸ¨hsx, thisâŸ©, hxâŸ©

  /-- The intersection of a closed set and a LindelÃ¶f set is a LindelÃ¶f set. -/
theorem IsLindelof.inter_left (ht : IsLindelof t) (hs : IsClosed s) : IsLindelof (s âˆ© t) :=
  inter_comm t s â–¸ ht.inter_right hs

  /-- The set difference of a LindelÃ¶f set and an open set is a LindelÃ¶f set. -/
theorem IsLindelof.diff (hs : IsLindelof s) (ht : IsOpen t) : IsLindelof (s \ t) :=
  hs.inter_right (isClosed_compl_iff.mpr ht)

/-- A closed subset of a LindelÃ¶f set is a LindelÃ¶f set. -/
theorem IsLindelof.of_isClosed_subset (hs : IsLindelof s) (ht : IsClosed t) (h : t âŠ† s) :
    IsLindelof t :=
  inter_eq_self_of_subset_right h â–¸ hs.inter_right ht

/-- A continuous image of a LindelÃ¶f set is a LindelÃ¶f set within its image. -/
theorem IsLindelof.image_of_continuousOn {f : X â†’ Y} (hs : IsLindelof s) (hf : ContinuousOn f s) :
    IsLindelof (f '' s) := by
  intro l lne _ ls
  have : NeBot (l.comap f âŠ“ ğ“Ÿ s) :=
    comap_inf_principal_neBot_of_image_mem lne (le_principal_iff.1 ls)
  obtain âŸ¨x, hxs, hxâŸ© : âˆƒ x âˆˆ s, ClusterPt x (l.comap f âŠ“ ğ“Ÿ s) := @hs _ this _ inf_le_right
  haveI := hx.neBot
  use f x, mem_image_of_mem f hxs
  have : Tendsto f (ğ“ x âŠ“ (comap f l âŠ“ ğ“Ÿ s)) (ğ“ (f x) âŠ“ l) := by
    convert (hf x hxs).inf (@tendsto_comap _ _ f l) using 1
    rw [nhdsWithin]
    ac_rfl
  exact this.neBot

/-- A continuous image of a LindelÃ¶f set is a LindelÃ¶f set within the codomain. -/
theorem IsLindelof.image {f : X â†’ Y} (hs : IsLindelof s) (hf : Continuous f) :
    IsLindelof (f '' s) := hs.image_of_continuousOn hf.continuousOn

/-- A filter with the countable intersection property that is finer than the principal filter on
a LindelÃ¶f set `s` contains any open set that contains all clusterpoints of `s`. -/
theorem IsLindelof.adherence_nhdset {f : Filter X} [CountableInterFilter f] (hs : IsLindelof s)
    (hfâ‚‚ : f â‰¤ ğ“Ÿ s) (htâ‚ : IsOpen t) (htâ‚‚ : âˆ€ x âˆˆ s, ClusterPt x f â†’ x âˆˆ t) : t âˆˆ f :=
  Classical.by_cases mem_of_eq_bot fun (this : f âŠ“ ğ“Ÿ tá¶œ â‰  âŠ¥) =>
  have hinf : CountableInterFilter (f âŠ“ ğ“Ÿ tá¶œ) := countableInterFilter_inf _ _
  let âŸ¨x, hx, (hfx : ClusterPt x <| f âŠ“ ğ“Ÿ tá¶œ)âŸ© := @hs _ âŸ¨thisâŸ© hinf <| inf_le_of_left_le hfâ‚‚
  have : x âˆˆ t := htâ‚‚ x hx hfx.of_inf_left
  have : tá¶œ âˆ© t âˆˆ ğ“[tá¶œ] x := inter_mem_nhdsWithin _ (IsOpen.mem_nhds htâ‚ this)
  have A : ğ“[tá¶œ] x = âŠ¥ := empty_mem_iff_bot.1 <| compl_inter_self t â–¸ this
  have : ğ“[tá¶œ] x â‰  âŠ¥ := hfx.of_inf_right.ne
  absurd A this

/--For every open cover of a LindelÃ¶f set, there exists a countable subcover. -/
theorem IsLindelof.elim_countable_subcover {Î¹ : Type v} (hs : IsLindelof s) (U : Î¹ â†’ Set X)
    (hUo : âˆ€ i, IsOpen (U i)) (hsU : s âŠ† â‹ƒ i, U i) :
    âˆƒ r : Set Î¹, r.Countable âˆ§ (s âŠ† â‹ƒ i âˆˆ r, U i) := by
  have he : âˆƒ r : Set Î¹, r.Countable âˆ§ âˆ… âŠ† â‹ƒ i âˆˆ r, U i := by use âˆ…; simp
  have hmono : âˆ€ â¦ƒs t : Set Xâ¦„, s âŠ† t â†’ (âˆƒ r : Set Î¹, r.Countable âˆ§ t âŠ† â‹ƒ i âˆˆ r, U i)
      â†’ (âˆƒ r : Set Î¹, r.Countable âˆ§ s âŠ† â‹ƒ i âˆˆ r, U i) := by
    intro _ _ hst âŸ¨r, âŸ¨hrcountable,hsubâŸ©âŸ©
    exact âŸ¨r,hrcountable,Subset.trans hst hsubâŸ©
  have hcountable_union : âˆ€ (S : Set (Set X)), S.Countable
      â†’ (âˆ€ s âˆˆ S, âˆƒ r : Set Î¹, r.Countable âˆ§ (s âŠ† â‹ƒ i âˆˆ r, U i))
      â†’ âˆƒ r : Set Î¹, r.Countable âˆ§ (â‹ƒ s âˆˆ S, s âŠ† â‹ƒ i âˆˆ r, U i) := by
    intro S hS hsr
    choose! r hr using hsr
    use â‹ƒ s âˆˆ S, r s
    constructor
    Â· refine (Countable.biUnion_iff hS).mpr ?h.left.a
      intro s hs
      apply (hr s hs).1
    Â· refine iUnionâ‚‚_subset ?h.right.h
      intro i
      simp
      intro is
      have h := (hr i is).2
      intro x hx
      exact mem_biUnion is (h hx)
  have h_nhds : âˆ€ x âˆˆ s, âˆƒ t âˆˆ ğ“[s] x, âˆƒ r : Set Î¹, r.Countable âˆ§ (t âŠ† â‹ƒ i âˆˆ r, U i) := by
    intro x hx
    let âŸ¨i, hiâŸ© := mem_iUnion.1 (hsU hx)
    refine' âŸ¨U i, mem_nhdsWithin_of_mem_nhds (IsOpen.mem_nhds (hUo i) hi),{i}, ?_âŸ©
    constructor <;> simp
    exact Subset.refl _
  exact hs.induction_on he hmono hcountable_union h_nhds

theorem IsLindelof.elim_nhds_subcover' (hs : IsLindelof s) (U : âˆ€ x âˆˆ s, Set X)
    (hU : âˆ€ x (hx : x âˆˆ s), U x â€¹x âˆˆ sâ€º âˆˆ ğ“ x) :
    âˆƒ t : Set s, t.Countable âˆ§ s âŠ† â‹ƒ x âˆˆ t, U (x : s) x.2 := by
  have := hs.elim_countable_subcover (fun x : s => interior (U x x.2)) (fun _ => isOpen_interior)
    fun x hx =>
      mem_iUnion.2 âŸ¨âŸ¨x, hxâŸ©, mem_interior_iff_mem_nhds.2 <| hU _ _âŸ©
  rcases this with âŸ¨r,âŸ¨hr,hsâŸ©âŸ©
  use r, hr
  apply Subset.trans hs
  apply iUnionâ‚‚_subset
  intro i hi
  apply Subset.trans interior_subset
  refine' subset_iUnion_of_subset i _
  refine' subset_iUnion_of_subset hi _
  apply Subset.refl

theorem IsLindelof.elim_nhds_subcover (hs : IsLindelof s) (U : X â†’ Set X)
    (hU : âˆ€ x âˆˆ s, U x âˆˆ ğ“ x) :
    âˆƒ t : Set X, t.Countable âˆ§ (âˆ€ x âˆˆ t, x âˆˆ s) âˆ§ s âŠ† â‹ƒ x âˆˆ t, U x := by
  let âŸ¨t, âŸ¨htc,htsubâŸ©âŸ© := hs.elim_nhds_subcover' (fun x _ => U x) hU
  use â†‘t
  constructor
  Â· exact Countable.image htc Subtype.val
  Â· constructor
    Â· intro x; simp; tauto
    Â· have : â‹ƒ x âˆˆ t, U â†‘x = â‹ƒ x âˆˆ Subtype.val '' t, U x := biUnion_image.symm
      rw [â† this]; assumption

/-- The neighborhood filter of a LindelÃ¶f set is disjoint with a filter `l` with the countable
intersection property if and only if the neighborhood filter of each point of this set
is disjoint with `l`. -/
theorem IsLindelof.disjoint_nhdsSet_left {l : Filter X} [CountableInterFilter l]
    (hs : IsLindelof s) :
    Disjoint (ğ“Ë¢ s) l â†” âˆ€ x âˆˆ s, Disjoint (ğ“ x) l := by
  refine' âŸ¨fun h x hx => h.mono_left <| nhds_le_nhdsSet hx, fun H => _âŸ©
  choose! U hxU hUl using fun x hx => (nhds_basis_opens x).disjoint_iff_left.1 (H x hx)
  choose hxU hUo using hxU
  rcases hs.elim_nhds_subcover U fun x hx => (hUo x hx).mem_nhds (hxU x hx) with âŸ¨t, htc, hts, hstâŸ©
  refine (hasBasis_nhdsSet _).disjoint_iff_left.2
    âŸ¨â‹ƒ x âˆˆ t, U x, âŸ¨isOpen_biUnion fun x hx => hUo x (hts x hx), hstâŸ©, ?_âŸ©
  rw [compl_iUnionâ‚‚]
  refine (countable_bInter_mem htc).mpr ?intro.intro.intro.a
  intro i hi
  apply hUl
  apply hts
  apply hi

/-- A filter `l` with the countable intersection property is disjoint with the neighborhood
filter of a LindelÃ¶f set if and only if it is disjoint with the neighborhood filter of each point
of this set. -/
theorem IsLindelof.disjoint_nhdsSet_right {l : Filter X} [CountableInterFilter l]
    (hs : IsLindelof s) : Disjoint l (ğ“Ë¢ s) â†” âˆ€ x âˆˆ s, Disjoint l (ğ“ x) := by
  simpa only [disjoint_comm] using hs.disjoint_nhdsSet_left

/-- For every family of closed sets whose intersection avoids a LindelÃ¶ set,
there exists a countable subfamily whose intersection avoids this LindelÃ¶f set. -/
theorem IsLindelof.elim_countable_subfamily_closed {Î¹ : Type v} (hs : IsLindelof s)
    (t : Î¹ â†’ Set X) (htc : âˆ€ i, IsClosed (t i)) (hst : (s âˆ© â‹‚ i, t i) = âˆ…) :
    âˆƒ u : Set Î¹, u.Countable âˆ§ (s âˆ© â‹‚ i âˆˆ u, t i) = âˆ… := by
    let U := tá¶œ
    have hUo : âˆ€ i, IsOpen (U i) := by simp; exact htc
    have hsU : s âŠ† â‹ƒ i, U i := by
      simp
      rw [â† compl_iInter]
      apply disjoint_compl_left_iff_subset.mp
      simp
      apply Disjoint.symm
      exact disjoint_iff_inter_eq_empty.mpr hst
    rcases hs.elim_countable_subcover U hUo hsU with âŸ¨u, âŸ¨hucount, husubâŸ©âŸ©
    use u, hucount
    rw [â† disjoint_compl_left_iff_subset] at husub
    simp at husub
    apply disjoint_iff_inter_eq_empty.mp
    apply Disjoint.symm
    assumption

/--To show that a LindelÃ¶f set intersects the intersection of a family of closed sets,
  it is sufficient to show that it intersects every countable subfamily. -/
theorem IsLindelof.inter_iInter_nonempty {Î¹ : Type v} (hs : IsLindelof s) (t : Î¹ â†’ Set X)
    (htc : âˆ€ i, IsClosed (t i)) (hst : âˆ€ u : Set Î¹, u.Countable âˆ§ (s âˆ© â‹‚ i âˆˆ u, t i).Nonempty) :
    (s âˆ© â‹‚ i, t i).Nonempty := by
  contrapose! hst
  rcases hs.elim_countable_subfamily_closed t htc hst with âŸ¨u, âŸ¨_, husubâŸ©âŸ©
  use u
  apply fun _ â†¦ husub

/-- For every open cover of a LindelÃ¶f set, there exists a countable subcover. -/
theorem IsLindelof.elim_countable_subcover_image {b : Set Î¹} {c : Î¹ â†’ Set X} (hs : IsLindelof s)
    (hcâ‚ : âˆ€ i âˆˆ b, IsOpen (c i)) (hcâ‚‚ : s âŠ† â‹ƒ i âˆˆ b, c i) :
    âˆƒ b', b' âŠ† b âˆ§ Set.Countable b' âˆ§ s âŠ† â‹ƒ i âˆˆ b', c i := by
  simp only [Subtype.forall', biUnion_eq_iUnion] at hcâ‚ hcâ‚‚
  rcases hs.elim_countable_subcover (fun i => c i : b â†’ Set X) hcâ‚ hcâ‚‚ with âŸ¨d, hdâŸ©
  refine' âŸ¨Subtype.val '' d, _, Countable.image hd.1 Subtype.val, _âŸ©-- d.image _, _âŸ©
  Â· simp
  Â· rw [biUnion_image]
    apply hd.2


/-- A set `s` is LindelÃ¶f if for every open cover of `s`, there exists a countable subcover. -/
theorem isLindelof_of_countable_subcover
    (h : âˆ€ {Î¹ : Type u} (U : Î¹ â†’ Set X), (âˆ€ i, IsOpen (U i)) â†’ (s âŠ† â‹ƒ i, U i) â†’
    âˆƒ t : Set Î¹, t.Countable âˆ§ s âŠ† â‹ƒ i âˆˆ t, U i) :
    IsLindelof s := fun f hf hfs => by
  contrapose! h
  simp only [ClusterPt, not_neBot, â† disjoint_iff, SetCoe.forall',
    (nhds_basis_opens _).disjoint_iff_left] at h
  choose fsub U hU hUf using h
  refine âŸ¨s, U, fun x => (hU x).2, fun x hx => mem_iUnion.2 âŸ¨âŸ¨x, hxâŸ©, (hU _).1 âŸ©, ?_ âŸ©
  intro t ht
  intro h
  have uinf := f.sets_of_superset (le_principal_iff.1 fsub) h
  have uninf : â‹‚ i âˆˆ t, (U i)á¶œ âˆˆ f := by refine (countable_bInter_mem ht).mpr (fun _ _ â†¦ hUf _)
  rw [â† compl_iUnionâ‚‚] at uninf
  have uninf := compl_not_mem uninf
  simp only [compl_compl] at uninf
  contradiction

/-- A set `s` is LindelÃ¶f if for every family of closed sets whose intersection avoids `s`,
there exists a countable subfamily whose intersection avoids `s`. -/
theorem isLindelof_of_countable_subfamily_closed
    (h : âˆ€ {Î¹ : Type u} (t : Î¹ â†’ Set X), (âˆ€ i, IsClosed (t i)) â†’ (s âˆ© â‹‚ i, t i) = âˆ… â†’
    âˆƒ u : Set Î¹, u.Countable âˆ§ (s âˆ© â‹‚ i âˆˆ u, t i) = âˆ…) :
    IsLindelof s :=
  isLindelof_of_countable_subcover fun U hUo hsU => by
    rw [â† disjoint_compl_right_iff_subset, compl_iUnion, disjoint_iff] at hsU
    rcases h (fun i => (U i)á¶œ) (fun i => (hUo _).isClosed_compl) hsU with âŸ¨t, htâŸ©
    refine âŸ¨t, ?_âŸ©
    rwa [â† disjoint_compl_right_iff_subset, compl_iUnionâ‚‚, disjoint_iff]

/-- A set `s` is LindelÃ¶f if and only if
for every open cover of `s`, there exists a countable subcover. -/
theorem isLindelof_iff_countable_subcover :
    IsLindelof s â†” âˆ€ {Î¹ : Type u} (U : Î¹ â†’ Set X),
      (âˆ€ i, IsOpen (U i)) â†’ (s âŠ† â‹ƒ i, U i) â†’ âˆƒ t : Set Î¹, t.Countable âˆ§ s âŠ† â‹ƒ i âˆˆ t, U i :=
  âŸ¨fun hs => hs.elim_countable_subcover, isLindelof_of_countable_subcoverâŸ©

/-- A set `s` is LindelÃ¶f if and only if
for every family of closed sets whose intersection avoids `s`,
there exists a countable subfamily whose intersection avoids `s`. -/
theorem isLindelof_iff_countable_subfamily_closed :
    IsLindelof s â†” âˆ€ {Î¹ : Type u} (t : Î¹ â†’ Set X),
    (âˆ€ i, IsClosed (t i)) â†’ (s âˆ© â‹‚ i, t i) = âˆ…
    â†’ âˆƒ u : Set Î¹, u.Countable âˆ§ (s âˆ© â‹‚ i âˆˆ u, t i) = âˆ… :=
  âŸ¨fun hs => hs.elim_countable_subfamily_closed, isLindelof_of_countable_subfamily_closedâŸ©

/-- A compact set `s` is LindelÃ¶f. -/
theorem IsCompact.isLindelof :
    IsCompact s â†’ IsLindelof s := by tauto
