/-
Copyright (c) 2023 Josha Dekker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Josha Dekker
-/
import Mathlib.Topology.Bases
import Mathlib.Order.Filter.CountableInter
import Mathlib.Topology.Compactness.SigmaCompact

/-!
# LindelÃ¶f sets and LindelÃ¶f spaces

## Main definitions

We define the following properties for sets in a topological space:

* `IsLindelof s`: Two definitions are possible here. The more standard definition is that
  every open cover that contains `s` contains a countable subcover. We choose for the equivalent
  definition where we require that every nontrivial filter on `s` with the countable intersection
  property has a clusterpoint. Equivalence is established in `isLindelof_iff_countable_subcover`.
* `LindelofSpace X`: `X` is LindelÃ¶f if it is LindelÃ¶f as a set.
* `NonLindelofSpace`: a space that is not a LindÃ«lof space, e.g. the Long Line.

## Main results

* `isLindelof_iff_countable_subcover`: A set is LindelÃ¶f iff every open cover has a
  countable subcover.

## Implementation details

* This API is mainly based on the API for IsCompact and follows notation and style as much
  as possible.
-/
open Set Filter Topology TopologicalSpace


universe u v

variable {X : Type u} {Y : Type v} {Î¹ : Type*}
variable [TopologicalSpace X] [TopologicalSpace Y] {s t : Set X}

section Lindelof

/-- A set `s` is LindelÃ¶f if every nontrivial filter `f` with the countable intersection
  property that contains `s`, has a clusterpoint in `s`. The filter-free definition is given by
  `isLindelof_iff_countable_subcover`. -/
def IsLindelof (s : Set X) :=
  âˆ€ â¦ƒfâ¦„ [NeBot f] [CountableInterFilter f], f â‰¤ ğ“Ÿ s â†’ âˆƒ x âˆˆ s, ClusterPt x f

/-- The complement to a LindelÃ¶f set belongs to a filter `f` with the countable intersection
  property if it belongs to each filter `ğ“ x âŠ“ f`, `x âˆˆ s`. -/
theorem IsLindelof.compl_mem_sets (hs : IsLindelof s) {f : Filter X} [CountableInterFilter f]
    (hf : âˆ€ x âˆˆ s, sá¶œ âˆˆ ğ“ x âŠ“ f) : sá¶œ âˆˆ f := by
  contrapose! hf
  simp only [notMem_iff_inf_principal_compl, compl_compl, inf_assoc] at hf âŠ¢
  exact hs inf_le_right

/-- The complement to a LindelÃ¶f set belongs to a filter `f` with the countable intersection
  property if each `x âˆˆ s` has a neighborhood `t` within `s` such that `tá¶œ` belongs to `f`. -/
theorem IsLindelof.compl_mem_sets_of_nhdsWithin (hs : IsLindelof s) {f : Filter X}
    [CountableInterFilter f] (hf : âˆ€ x âˆˆ s, âˆƒ t âˆˆ ğ“[s] x, tá¶œ âˆˆ f) : sá¶œ âˆˆ f := by
  refine hs.compl_mem_sets fun x hx â†¦ ?_
  rw [â† disjoint_principal_right, disjoint_right_comm, (basis_sets _).disjoint_iff_left]
  exact hf x hx

/-- If `p : Set X â†’ Prop` is stable under restriction and union, and each point `x`
  of a LindelÃ¶f set `s` has a neighborhood `t` within `s` such that `p t`, then `p s` holds. -/
@[elab_as_elim]
theorem IsLindelof.induction_on (hs : IsLindelof s) {p : Set X â†’ Prop}
    (hmono : âˆ€ â¦ƒs tâ¦„, s âŠ† t â†’ p t â†’ p s)
    (hcountable_union : âˆ€ (S : Set (Set X)), S.Countable â†’ (âˆ€ s âˆˆ S, p s) â†’ p (â‹ƒâ‚€ S))
    (hnhds : âˆ€ x âˆˆ s, âˆƒ t âˆˆ ğ“[s] x, p t) : p s := by
  let f : Filter X := ofCountableUnion p hcountable_union (fun t ht _ hsub â†¦ hmono hsub ht)
  have : sá¶œ âˆˆ f := hs.compl_mem_sets_of_nhdsWithin (by simpa [f] using hnhds)
  rwa [â† compl_compl s]

/-- The intersection of a LindelÃ¶f set and a closed set is a LindelÃ¶f set. -/
theorem IsLindelof.inter_right (hs : IsLindelof s) (ht : IsClosed t) : IsLindelof (s âˆ© t) := by
  intro f hnf _ hstf
  rw [â† inf_principal, le_inf_iff] at hstf
  obtain âŸ¨x, hsx, hxâŸ© : âˆƒ x âˆˆ s, ClusterPt x f := hs hstf.1
  have hxt : x âˆˆ t := ht.mem_of_nhdsWithin_neBot <| hx.mono hstf.2
  exact âŸ¨x, âŸ¨hsx, hxtâŸ©, hxâŸ©

/-- The intersection of a closed set and a LindelÃ¶f set is a LindelÃ¶f set. -/
theorem IsLindelof.inter_left (ht : IsLindelof t) (hs : IsClosed s) : IsLindelof (s âˆ© t) :=
  inter_comm t s â–¸ ht.inter_right hs

/-- The set difference of a LindelÃ¶f set and an open set is a LindelÃ¶f set. -/
theorem IsLindelof.diff (hs : IsLindelof s) (ht : IsOpen t) : IsLindelof (s \ t) :=
  hs.inter_right (isClosed_compl_iff.mpr ht)

/-- A closed subset of a LindelÃ¶f set is a LindelÃ¶f set. -/
theorem IsLindelof.of_isClosed_subset (hs : IsLindelof s) (ht : IsClosed t) (h : t âŠ† s) :
    IsLindelof t := inter_eq_self_of_subset_right h â–¸ hs.inter_right ht

/-- A continuous image of a LindelÃ¶f set is a LindelÃ¶f set. -/
theorem IsLindelof.image_of_continuousOn {f : X â†’ Y} (hs : IsLindelof s) (hf : ContinuousOn f s) :
    IsLindelof (f '' s) := by
  intro l lne _ ls
  have : NeBot (l.comap f âŠ“ ğ“Ÿ s) :=
    comap_inf_principal_neBot_of_image_mem lne (le_principal_iff.1 ls)
  obtain âŸ¨x, hxs, hxâŸ© : âˆƒ x âˆˆ s, ClusterPt x (l.comap f âŠ“ ğ“Ÿ s) := @hs _ this _ inf_le_right
  haveI := hx.neBot
  use f x, mem_image_of_mem f hxs
  have : Tendsto f (ğ“ x âŠ“ (comap f l âŠ“ ğ“Ÿ s)) (ğ“ (f x) âŠ“ l) := by
    convert (hf x hxs).inf (@tendsto_comap _ _ f l) using 1
    rw [nhdsWithin]
    ac_rfl
  exact this.neBot

/-- A continuous image of a LindelÃ¶f set is a LindelÃ¶f set within the codomain. -/
theorem IsLindelof.image {f : X â†’ Y} (hs : IsLindelof s) (hf : Continuous f) :
    IsLindelof (f '' s) := hs.image_of_continuousOn hf.continuousOn

/-- A filter with the countable intersection property that is finer than the principal filter on
a LindelÃ¶f set `s` contains any open set that contains all clusterpoints of `s`. -/
theorem IsLindelof.adherence_nhdset {f : Filter X} [CountableInterFilter f] (hs : IsLindelof s)
    (hfâ‚‚ : f â‰¤ ğ“Ÿ s) (htâ‚ : IsOpen t) (htâ‚‚ : âˆ€ x âˆˆ s, ClusterPt x f â†’ x âˆˆ t) : t âˆˆ f :=
  (eq_or_neBot _).casesOn mem_of_eq_bot fun _ â†¦
    let âŸ¨x, hx, hfxâŸ© := @hs (f âŠ“ ğ“Ÿ tá¶œ) _ _ <| inf_le_of_left_le hfâ‚‚
    have : x âˆˆ t := htâ‚‚ x hx hfx.of_inf_left
    have : tá¶œ âˆ© t âˆˆ ğ“[tá¶œ] x := inter_mem_nhdsWithin _ (htâ‚.mem_nhds this)
    have A : ğ“[tá¶œ] x = âŠ¥ := empty_mem_iff_bot.1 <| compl_inter_self t â–¸ this
    have : ğ“[tá¶œ] x â‰  âŠ¥ := hfx.of_inf_right.ne
    absurd A this

/-- For every open cover of a LindelÃ¶f set, there exists a countable subcover. -/
theorem IsLindelof.elim_countable_subcover {Î¹ : Type v} (hs : IsLindelof s) (U : Î¹ â†’ Set X)
    (hUo : âˆ€ i, IsOpen (U i)) (hsU : s âŠ† â‹ƒ i, U i) :
    âˆƒ r : Set Î¹, r.Countable âˆ§ (s âŠ† â‹ƒ i âˆˆ r, U i) := by
  have hmono : âˆ€ â¦ƒs t : Set Xâ¦„, s âŠ† t â†’ (âˆƒ r : Set Î¹, r.Countable âˆ§ t âŠ† â‹ƒ i âˆˆ r, U i)
      â†’ (âˆƒ r : Set Î¹, r.Countable âˆ§ s âŠ† â‹ƒ i âˆˆ r, U i) := by
    intro _ _ hst âŸ¨r, âŸ¨hrcountable, hsubâŸ©âŸ©
    exact âŸ¨r, hrcountable, Subset.trans hst hsubâŸ©
  have hcountable_union : âˆ€ (S : Set (Set X)), S.Countable
      â†’ (âˆ€ s âˆˆ S, âˆƒ r : Set Î¹, r.Countable âˆ§ (s âŠ† â‹ƒ i âˆˆ r, U i))
      â†’ âˆƒ r : Set Î¹, r.Countable âˆ§ (â‹ƒâ‚€ S âŠ† â‹ƒ i âˆˆ r, U i) := by
    intro S hS hsr
    choose! r hr using hsr
    refine âŸ¨â‹ƒ s âˆˆ S, r s, hS.biUnion_iff.mpr (fun s hs â†¦ (hr s hs).1), ?_âŸ©
    refine sUnion_subset ?h.right.h
    simp only [mem_iUnion, exists_prop, iUnion_exists, biUnion_and']
    exact fun i is x hx â†¦ mem_biUnion is ((hr i is).2 hx)
  have h_nhds : âˆ€ x âˆˆ s, âˆƒ t âˆˆ ğ“[s] x, âˆƒ r : Set Î¹, r.Countable âˆ§ (t âŠ† â‹ƒ i âˆˆ r, U i) := by
    intro x hx
    let âŸ¨i, hiâŸ© := mem_iUnion.1 (hsU hx)
    refine âŸ¨U i, mem_nhdsWithin_of_mem_nhds ((hUo i).mem_nhds hi), {i}, by simp, ?_âŸ©
    simp only [mem_singleton_iff, iUnion_iUnion_eq_left]
    exact Subset.refl _
  exact hs.induction_on hmono hcountable_union h_nhds

theorem IsLindelof.elim_nhds_subcover' (hs : IsLindelof s) (U : âˆ€ x âˆˆ s, Set X)
    (hU : âˆ€ x (hx : x âˆˆ s), U x â€¹x âˆˆ sâ€º âˆˆ ğ“ x) :
    âˆƒ t : Set s, t.Countable âˆ§ s âŠ† â‹ƒ x âˆˆ t, U (x : s) x.2 := by
  have := hs.elim_countable_subcover (fun x : s â†¦ interior (U x x.2)) (fun _ â†¦ isOpen_interior)
    fun x hx â†¦
      mem_iUnion.2 âŸ¨âŸ¨x, hxâŸ©, mem_interior_iff_mem_nhds.2 <| hU _ _âŸ©
  rcases this with âŸ¨r, âŸ¨hr, hsâŸ©âŸ©
  use r, hr
  apply Subset.trans hs
  apply iUnionâ‚‚_subset
  intro i hi
  apply Subset.trans interior_subset
  exact subset_iUnion_of_subset i (subset_iUnion_of_subset hi (Subset.refl _))

theorem IsLindelof.elim_nhds_subcover (hs : IsLindelof s) (U : X â†’ Set X)
    (hU : âˆ€ x âˆˆ s, U x âˆˆ ğ“ x) :
    âˆƒ t : Set X, t.Countable âˆ§ (âˆ€ x âˆˆ t, x âˆˆ s) âˆ§ s âŠ† â‹ƒ x âˆˆ t, U x := by
  let âŸ¨t, âŸ¨htc, htsubâŸ©âŸ© := hs.elim_nhds_subcover' (fun x _ â†¦ U x) hU
  refine âŸ¨â†‘t, Countable.image htc Subtype.val, ?_âŸ©
  constructor
  Â· intro _
    simp only [mem_image, Subtype.exists, exists_and_right, exists_eq_right, forall_exists_index]
    tauto
  Â· have : â‹ƒ x âˆˆ t, U â†‘x = â‹ƒ x âˆˆ Subtype.val '' t, U x := biUnion_image.symm
    rwa [â† this]

/-- For every nonempty open cover of a LindelÃ¶f set, there exists a subcover indexed by â„•. -/
theorem IsLindelof.indexed_countable_subcover {Î¹ : Type v} [Nonempty Î¹]
    (hs : IsLindelof s) (U : Î¹ â†’ Set X) (hUo : âˆ€ i, IsOpen (U i)) (hsU : s âŠ† â‹ƒ i, U i) :
    âˆƒ f : â„• â†’ Î¹, s âŠ† â‹ƒ n, U (f n) := by
  obtain âŸ¨c, âŸ¨c_count, c_covâŸ©âŸ© := hs.elim_countable_subcover U hUo hsU
  rcases c.eq_empty_or_nonempty with rfl | c_nonempty
  Â· simp only [mem_empty_iff_false, iUnion_of_empty, iUnion_empty] at c_cov
    simp only [subset_eq_empty c_cov rfl, empty_subset, exists_const]
  obtain âŸ¨f, f_surjâŸ© := (Set.countable_iff_exists_surjective c_nonempty).mp c_count
  refine âŸ¨fun x â†¦ f x, c_cov.trans <| iUnionâ‚‚_subset_iff.mpr (?_ : âˆ€ i âˆˆ c, U i âŠ† â‹ƒ n, U (f n))âŸ©
  intro x hx
  obtain âŸ¨n, hnâŸ© := f_surj âŸ¨x, hxâŸ©
  exact subset_iUnion_of_subset n <| subset_of_eq (by rw [hn])

/-- The neighborhood filter of a LindelÃ¶f set is disjoint with a filter `l` with the countable
intersection property if and only if the neighborhood filter of each point of this set
is disjoint with `l`. -/
theorem IsLindelof.disjoint_nhdsSet_left {l : Filter X} [CountableInterFilter l]
    (hs : IsLindelof s) :
    Disjoint (ğ“Ë¢ s) l â†” âˆ€ x âˆˆ s, Disjoint (ğ“ x) l := by
  refine âŸ¨fun h x hx â†¦ h.mono_left <| nhds_le_nhdsSet hx, fun H â†¦ ?_âŸ©
  choose! U hxU hUl using fun x hx â†¦ (nhds_basis_opens x).disjoint_iff_left.1 (H x hx)
  choose hxU hUo using hxU
  rcases hs.elim_nhds_subcover U fun x hx â†¦ (hUo x hx).mem_nhds (hxU x hx) with âŸ¨t, htc, hts, hstâŸ©
  refine (hasBasis_nhdsSet _).disjoint_iff_left.2
    âŸ¨â‹ƒ x âˆˆ t, U x, âŸ¨isOpen_biUnion fun x hx â†¦ hUo x (hts x hx), hstâŸ©, ?_âŸ©
  rw [compl_iUnionâ‚‚]
  exact (countable_bInter_mem htc).mpr (fun i hi â†¦ hUl _ (hts _ hi))

/-- A filter `l` with the countable intersection property is disjoint with the neighborhood
filter of a LindelÃ¶f set if and only if it is disjoint with the neighborhood filter of each point
of this set. -/
theorem IsLindelof.disjoint_nhdsSet_right {l : Filter X} [CountableInterFilter l]
    (hs : IsLindelof s) : Disjoint l (ğ“Ë¢ s) â†” âˆ€ x âˆˆ s, Disjoint l (ğ“ x) := by
  simpa only [disjoint_comm] using hs.disjoint_nhdsSet_left

/-- For every family of closed sets whose intersection avoids a LindelÃ¶f set,
there exists a countable subfamily whose intersection avoids this LindelÃ¶f set. -/
theorem IsLindelof.elim_countable_subfamily_closed {Î¹ : Type v} (hs : IsLindelof s)
    (t : Î¹ â†’ Set X) (htc : âˆ€ i, IsClosed (t i)) (hst : (s âˆ© â‹‚ i, t i) = âˆ…) :
    âˆƒ u : Set Î¹, u.Countable âˆ§ (s âˆ© â‹‚ i âˆˆ u, t i) = âˆ… := by
  let U := tá¶œ
  have hUo : âˆ€ i, IsOpen (U i) := by simp only [U, Pi.compl_apply, isOpen_compl_iff]; exact htc
  have hsU : s âŠ† â‹ƒ i, U i := by
    simp only [U, Pi.compl_apply]
    rw [â† compl_iInter]
    apply disjoint_compl_left_iff_subset.mp
    simp only [compl_iInter, compl_iUnion, compl_compl]
    apply Disjoint.symm
    exact disjoint_iff_inter_eq_empty.mpr hst
  rcases hs.elim_countable_subcover U hUo hsU with âŸ¨u, âŸ¨hucount, husubâŸ©âŸ©
  use u, hucount
  rw [â† disjoint_compl_left_iff_subset] at husub
  simp only [U, Pi.compl_apply, compl_iUnion, compl_compl] at husub
  exact disjoint_iff_inter_eq_empty.mp (Disjoint.symm husub)

/-- To show that a LindelÃ¶f set intersects the intersection of a family of closed sets,
  it is sufficient to show that it intersects every countable subfamily. -/
theorem IsLindelof.inter_iInter_nonempty {Î¹ : Type v} (hs : IsLindelof s) (t : Î¹ â†’ Set X)
    (htc : âˆ€ i, IsClosed (t i)) (hst : âˆ€ u : Set Î¹, u.Countable âˆ§ (s âˆ© â‹‚ i âˆˆ u, t i).Nonempty) :
    (s âˆ© â‹‚ i, t i).Nonempty := by
  contrapose! hst
  rcases hs.elim_countable_subfamily_closed t htc hst with âŸ¨u, âŸ¨_, husubâŸ©âŸ©
  exact âŸ¨u, fun _ â†¦ husubâŸ©

/-- For every open cover of a LindelÃ¶f set, there exists a countable subcover. -/
theorem IsLindelof.elim_countable_subcover_image {b : Set Î¹} {c : Î¹ â†’ Set X} (hs : IsLindelof s)
    (hcâ‚ : âˆ€ i âˆˆ b, IsOpen (c i)) (hcâ‚‚ : s âŠ† â‹ƒ i âˆˆ b, c i) :
    âˆƒ b', b' âŠ† b âˆ§ Set.Countable b' âˆ§ s âŠ† â‹ƒ i âˆˆ b', c i := by
  simp only [Subtype.forall', biUnion_eq_iUnion] at hcâ‚ hcâ‚‚
  rcases hs.elim_countable_subcover (fun i â†¦ c i : b â†’ Set X) hcâ‚ hcâ‚‚ with âŸ¨d, hdâŸ©
  refine âŸ¨Subtype.val '' d, by simp, Countable.image hd.1 Subtype.val, ?_âŸ©
  rw [biUnion_image]
  exact hd.2


/-- A set `s` is LindelÃ¶f if for every open cover of `s`, there exists a countable subcover. -/
theorem isLindelof_of_countable_subcover
    (h : âˆ€ {Î¹ : Type u} (U : Î¹ â†’ Set X), (âˆ€ i, IsOpen (U i)) â†’ (s âŠ† â‹ƒ i, U i) â†’
    âˆƒ t : Set Î¹, t.Countable âˆ§ s âŠ† â‹ƒ i âˆˆ t, U i) :
    IsLindelof s := fun f hf hfs â†¦ by
  contrapose! h
  simp only [ClusterPt, not_neBot, â† disjoint_iff, SetCoe.forall',
    (nhds_basis_opens _).disjoint_iff_left] at h
  choose fsub U hU hUf using h
  refine âŸ¨s, U, fun x â†¦ (hU x).2, fun x hx â†¦ mem_iUnion.2 âŸ¨âŸ¨x, hxâŸ©, (hU _).1 âŸ©, ?_âŸ©
  intro t ht h
  have uinf := f.sets_of_superset (le_principal_iff.1 fsub) h
  have uninf : â‹‚ i âˆˆ t, (U i)á¶œ âˆˆ f := (countable_bInter_mem ht).mpr (fun _ _ â†¦ hUf _)
  rw [â† compl_iUnionâ‚‚] at uninf
  have uninf := compl_notMem uninf
  simp only [compl_compl] at uninf
  contradiction

/-- A set `s` is LindelÃ¶f if for every family of closed sets whose intersection avoids `s`,
there exists a countable subfamily whose intersection avoids `s`. -/
theorem isLindelof_of_countable_subfamily_closed
    (h :
      âˆ€ {Î¹ : Type u} (t : Î¹ â†’ Set X), (âˆ€ i, IsClosed (t i)) â†’ (s âˆ© â‹‚ i, t i) = âˆ… â†’
        âˆƒ u : Set Î¹, u.Countable âˆ§ (s âˆ© â‹‚ i âˆˆ u, t i) = âˆ…) :
    IsLindelof s :=
  isLindelof_of_countable_subcover fun U hUo hsU â†¦ by
    rw [â† disjoint_compl_right_iff_subset, compl_iUnion, disjoint_iff] at hsU
    rcases h (fun i â†¦ (U i)á¶œ) (fun i â†¦ (hUo _).isClosed_compl) hsU with âŸ¨t, htâŸ©
    refine âŸ¨t, ?_âŸ©
    rwa [â† disjoint_compl_right_iff_subset, compl_iUnionâ‚‚, disjoint_iff]

/-- A set `s` is LindelÃ¶f if and only if
for every open cover of `s`, there exists a countable subcover. -/
theorem isLindelof_iff_countable_subcover :
    IsLindelof s â†” âˆ€ {Î¹ : Type u} (U : Î¹ â†’ Set X),
      (âˆ€ i, IsOpen (U i)) â†’ (s âŠ† â‹ƒ i, U i) â†’ âˆƒ t : Set Î¹, t.Countable âˆ§ s âŠ† â‹ƒ i âˆˆ t, U i :=
  âŸ¨fun hs â†¦ hs.elim_countable_subcover, isLindelof_of_countable_subcoverâŸ©

/-- A set `s` is LindelÃ¶f if and only if
for every family of closed sets whose intersection avoids `s`,
there exists a countable subfamily whose intersection avoids `s`. -/
theorem isLindelof_iff_countable_subfamily_closed :
    IsLindelof s â†” âˆ€ {Î¹ : Type u} (t : Î¹ â†’ Set X),
    (âˆ€ i, IsClosed (t i)) â†’ (s âˆ© â‹‚ i, t i) = âˆ…
    â†’ âˆƒ u : Set Î¹, u.Countable âˆ§ (s âˆ© â‹‚ i âˆˆ u, t i) = âˆ… :=
  âŸ¨fun hs â†¦ hs.elim_countable_subfamily_closed, isLindelof_of_countable_subfamily_closedâŸ©

/-- The empty set is a Lindelof set. -/
@[simp]
theorem isLindelof_empty : IsLindelof (âˆ… : Set X) := fun _f hnf _ hsf â†¦
  Not.elim hnf.ne <| empty_mem_iff_bot.1 <| le_principal_iff.1 hsf

/-- A singleton set is a Lindelof set. -/
@[simp]
theorem isLindelof_singleton {x : X} : IsLindelof ({x} : Set X) := fun _ hf _ hfa â†¦
  âŸ¨x, rfl, ClusterPt.of_le_nhds'
    (hfa.trans <| by simpa only [principal_singleton] using pure_le_nhds x) hfâŸ©

theorem Set.Subsingleton.isLindelof (hs : s.Subsingleton) : IsLindelof s :=
  Subsingleton.induction_on hs isLindelof_empty fun _ â†¦ isLindelof_singleton

theorem Set.Countable.isLindelof_biUnion {s : Set Î¹} {f : Î¹ â†’ Set X} (hs : s.Countable)
    (hf : âˆ€ i âˆˆ s, IsLindelof (f i)) : IsLindelof (â‹ƒ i âˆˆ s, f i) := by
  apply isLindelof_of_countable_subcover
  intro i U hU hUcover
  have hiU : âˆ€ i âˆˆ s, f i âŠ† â‹ƒ i, U i :=
    fun _ is â†¦ _root_.subset_trans (subset_biUnion_of_mem is) hUcover
  have iSets := fun i is â†¦ (hf i is).elim_countable_subcover U hU (hiU i is)
  choose! r hr using iSets
  use â‹ƒ i âˆˆ s, r i
  constructor
  Â· refine (Countable.biUnion_iff hs).mpr ?h.left.a
    exact fun s hs â†¦ (hr s hs).1
  Â· refine iUnionâ‚‚_subset ?h.right.h
    intro i is
    simp only [mem_iUnion, exists_prop, iUnion_exists, biUnion_and']
    intro x hx
    exact mem_biUnion is ((hr i is).2 hx)


theorem Set.Finite.isLindelof_biUnion {s : Set Î¹} {f : Î¹ â†’ Set X} (hs : s.Finite)
    (hf : âˆ€ i âˆˆ s, IsLindelof (f i)) : IsLindelof (â‹ƒ i âˆˆ s, f i) :=
  Set.Countable.isLindelof_biUnion (countable hs) hf

theorem Finset.isLindelof_biUnion (s : Finset Î¹) {f : Î¹ â†’ Set X} (hf : âˆ€ i âˆˆ s, IsLindelof (f i)) :
    IsLindelof (â‹ƒ i âˆˆ s, f i) :=
  s.finite_toSet.isLindelof_biUnion hf

theorem isLindelof_accumulate {K : â„• â†’ Set X} (hK : âˆ€ n, IsLindelof (K n)) (n : â„•) :
    IsLindelof (Accumulate K n) :=
  (finite_le_nat n).isLindelof_biUnion fun k _ => hK k

theorem Set.Countable.isLindelof_sUnion {S : Set (Set X)} (hf : S.Countable)
    (hc : âˆ€ s âˆˆ S, IsLindelof s) : IsLindelof (â‹ƒâ‚€ S) := by
  rw [sUnion_eq_biUnion]; exact hf.isLindelof_biUnion hc

theorem Set.Finite.isLindelof_sUnion {S : Set (Set X)} (hf : S.Finite)
    (hc : âˆ€ s âˆˆ S, IsLindelof s) : IsLindelof (â‹ƒâ‚€ S) := by
  rw [sUnion_eq_biUnion]; exact hf.isLindelof_biUnion hc

theorem isLindelof_iUnion {Î¹ : Sort*} {f : Î¹ â†’ Set X} [Countable Î¹] (h : âˆ€ i, IsLindelof (f i)) :
    IsLindelof (â‹ƒ i, f i) := (countable_range f).isLindelof_sUnion  <| forall_mem_range.2 h

theorem Set.Countable.isLindelof (hs : s.Countable) : IsLindelof s :=
  biUnion_of_singleton s â–¸ hs.isLindelof_biUnion fun _ _ => isLindelof_singleton

theorem Set.Finite.isLindelof (hs : s.Finite) : IsLindelof s :=
  biUnion_of_singleton s â–¸ hs.isLindelof_biUnion fun _ _ => isLindelof_singleton

theorem IsLindelof.countable_of_discrete [DiscreteTopology X] (hs : IsLindelof s) :
    s.Countable := by
  have : âˆ€ x : X, ({x} : Set X) âˆˆ ğ“ x := by simp [nhds_discrete]
  rcases hs.elim_nhds_subcover (fun x => {x}) fun x _ => this x with âŸ¨t, ht, _, hssubtâŸ©
  rw [biUnion_of_singleton] at hssubt
  exact ht.mono hssubt

theorem isLindelof_iff_countable [DiscreteTopology X] : IsLindelof s â†” s.Countable :=
  âŸ¨fun h => h.countable_of_discrete, fun h => h.isLindelofâŸ©

theorem IsLindelof.union (hs : IsLindelof s) (ht : IsLindelof t) : IsLindelof (s âˆª t) := by
  rw [union_eq_iUnion]; exact isLindelof_iUnion fun b => by cases b <;> assumption

protected theorem IsLindelof.insert (hs : IsLindelof s) (a) : IsLindelof (insert a s) :=
  isLindelof_singleton.union hs

/-- If `X` has a basis consisting of compact opens, then an open set in `X` is compact open iff
it is a finite union of some elements in the basis -/
theorem isLindelof_open_iff_eq_countable_iUnion_of_isTopologicalBasis (b : Î¹ â†’ Set X)
    (hb : IsTopologicalBasis (Set.range b)) (hb' : âˆ€ i, IsLindelof (b i)) (U : Set X) :
    IsLindelof U âˆ§ IsOpen U â†” âˆƒ s : Set Î¹, s.Countable âˆ§ U = â‹ƒ i âˆˆ s, b i := by
  constructor
  Â· rintro âŸ¨hâ‚, hâ‚‚âŸ©
    obtain âŸ¨Y, f, rfl, hfâŸ© := hb.open_eq_iUnion hâ‚‚
    choose f' hf' using hf
    have : b âˆ˜ f' = f := funext hf'
    subst this
    obtain âŸ¨t, htâŸ© :=
      hâ‚.elim_countable_subcover (b âˆ˜ f') (fun i => hb.isOpen (Set.mem_range_self _)) Subset.rfl
    refine âŸ¨t.image f', Countable.image (ht.1) f', le_antisymm ?_ ?_âŸ©
    Â· refine Set.Subset.trans ht.2 ?_
      simp only [Set.iUnion_subset_iff]
      intro i hi
      rw [â† Set.iUnion_subtype (fun x : Î¹ => x âˆˆ t.image f') fun i => b i.1]
      exact Set.subset_iUnion (fun i : t.image f' => b i) âŸ¨_, mem_image_of_mem _ hiâŸ©
    Â· apply Set.iUnionâ‚‚_subset
      rintro i hi
      obtain âŸ¨j, -, rflâŸ© := (mem_image ..).mp hi
      exact Set.subset_iUnion (b âˆ˜ f') j
  Â· rintro âŸ¨s, hs, rflâŸ©
    constructor
    Â· exact hs.isLindelof_biUnion fun i _ => hb' i
    Â· exact isOpen_biUnion fun i _ => hb.isOpen (Set.mem_range_self _)

/-- `Filter.coLindelof` is the filter generated by complements to LindelÃ¶f sets. -/
def Filter.coLindelof (X : Type*) [TopologicalSpace X] : Filter X :=
  --`Filter.coLindelof` is the filter generated by complements to LindelÃ¶f sets.
  â¨… (s : Set X) (_ : IsLindelof s), ğ“Ÿ sá¶œ

theorem hasBasis_coLindelof : (coLindelof X).HasBasis IsLindelof compl :=
  hasBasis_biInf_principal'
    (fun s hs t ht =>
      âŸ¨s âˆª t, hs.union ht, compl_subset_compl.2 subset_union_left,
        compl_subset_compl.2 subset_union_rightâŸ©)
    âŸ¨âˆ…, isLindelof_emptyâŸ©

theorem mem_coLindelof : s âˆˆ coLindelof X â†” âˆƒ t, IsLindelof t âˆ§ tá¶œ âŠ† s :=
  hasBasis_coLindelof.mem_iff

theorem mem_coLindelof' : s âˆˆ coLindelof X â†” âˆƒ t, IsLindelof t âˆ§ sá¶œ âŠ† t :=
  mem_coLindelof.trans <| exists_congr fun _ => and_congr_right fun _ => compl_subset_comm

theorem _root_.IsLindelof.compl_mem_coLindelof (hs : IsLindelof s) : sá¶œ âˆˆ coLindelof X :=
  hasBasis_coLindelof.mem_of_mem hs

theorem coLindelof_le_cofinite : coLindelof X â‰¤ cofinite := fun s hs =>
  compl_compl s â–¸ hs.isLindelof.compl_mem_coLindelof

theorem Tendsto.isLindelof_insert_range_of_coLindelof {f : X â†’ Y} {y}
    (hf : Tendsto f (coLindelof X) (ğ“ y)) (hfc : Continuous f) :
    IsLindelof (insert y (range f)) := by
  intro l hne _ hle
  by_cases hy : ClusterPt y l
  Â· exact âŸ¨y, Or.inl rfl, hyâŸ©
  simp only [clusterPt_iff_nonempty, not_forall, â† not_disjoint_iff_nonempty_inter, not_not] at hy
  rcases hy with âŸ¨s, hsy, t, htl, hdâŸ©
  rcases mem_coLindelof.1 (hf hsy) with âŸ¨K, hKc, hKsâŸ©
  have : f '' K âˆˆ l := by
    filter_upwards [htl, le_principal_iff.1 hle] with y hyt hyf
    rcases hyf with (rfl | âŸ¨x, rflâŸ©)
    exacts [(hd.le_bot âŸ¨mem_of_mem_nhds hsy, hytâŸ©).elim,
      mem_image_of_mem _ (not_not.1 fun hxK => hd.le_bot âŸ¨hKs hxK, hytâŸ©)]
  rcases hKc.image hfc (le_principal_iff.2 this) with âŸ¨y, hy, hylâŸ©
  exact âŸ¨y, Or.inr <| image_subset_range _ _ hy, hylâŸ©

/-- `Filter.coclosedLindelof` is the filter generated by complements to closed Lindelof sets. -/
def Filter.coclosedLindelof (X : Type*) [TopologicalSpace X] : Filter X :=
  -- `Filter.coclosedLindelof` is the filter generated by complements to closed Lindelof sets.
  â¨… (s : Set X) (_ : IsClosed s) (_ : IsLindelof s), ğ“Ÿ sá¶œ

theorem hasBasis_coclosedLindelof :
    (Filter.coclosedLindelof X).HasBasis (fun s => IsClosed s âˆ§ IsLindelof s) compl := by
  simp only [Filter.coclosedLindelof, iInf_and']
  refine hasBasis_biInf_principal' ?_ âŸ¨âˆ…, isClosed_empty, isLindelof_emptyâŸ©
  rintro s âŸ¨hsâ‚, hsâ‚‚âŸ© t âŸ¨htâ‚, htâ‚‚âŸ©
  exact âŸ¨s âˆª t, âŸ¨âŸ¨hsâ‚.union htâ‚, hsâ‚‚.union htâ‚‚âŸ©, compl_subset_compl.2 subset_union_left,
    compl_subset_compl.2 subset_union_rightâŸ©âŸ©

theorem mem_coclosedLindelof : s âˆˆ coclosedLindelof X â†”
    âˆƒ t, IsClosed t âˆ§ IsLindelof t âˆ§ tá¶œ âŠ† s := by
  simp only [hasBasis_coclosedLindelof.mem_iff, and_assoc]

theorem mem_coclosed_Lindelof' : s âˆˆ coclosedLindelof X â†”
    âˆƒ t, IsClosed t âˆ§ IsLindelof t âˆ§ sá¶œ âŠ† t := by
  simp only [mem_coclosedLindelof, compl_subset_comm]

theorem coLindelof_le_coclosedLindelof : coLindelof X â‰¤ coclosedLindelof X :=
  iInf_mono fun _ => le_iInf fun _ => le_rfl

theorem IsLindeof.compl_mem_coclosedLindelof_of_isClosed (hs : IsLindelof s) (hs' : IsClosed s) :
    sá¶œ âˆˆ Filter.coclosedLindelof X :=
  hasBasis_coclosedLindelof.mem_of_mem âŸ¨hs', hsâŸ©

/-- X is a LindelÃ¶f space iff every open cover has a countable subcover. -/
class LindelofSpace (X : Type*) [TopologicalSpace X] : Prop where
  /-- In a LindelÃ¶f space, `Set.univ` is a LindelÃ¶f set. -/
  isLindelof_univ : IsLindelof (univ : Set X)

instance (priority := 10) Subsingleton.lindelofSpace [Subsingleton X] : LindelofSpace X :=
  âŸ¨subsingleton_univ.isLindelofâŸ©

theorem isLindelof_univ_iff : IsLindelof (univ : Set X) â†” LindelofSpace X :=
  âŸ¨fun h => âŸ¨hâŸ©, fun h => h.1âŸ©

theorem isLindelof_univ [h : LindelofSpace X] : IsLindelof (univ : Set X) :=
  h.isLindelof_univ

theorem cluster_point_of_Lindelof [LindelofSpace X] (f : Filter X) [NeBot f]
    [CountableInterFilter f] : âˆƒ x, ClusterPt x f := by
  simpa using isLindelof_univ (show f â‰¤ ğ“Ÿ univ by simp)

theorem LindelofSpace.elim_nhds_subcover [LindelofSpace X] (U : X â†’ Set X) (hU : âˆ€ x, U x âˆˆ ğ“ x) :
    âˆƒ t : Set X, t.Countable âˆ§ â‹ƒ x âˆˆ t, U x = univ := by
  obtain âŸ¨t, tc, -, sâŸ© := IsLindelof.elim_nhds_subcover isLindelof_univ U fun x _ => hU x
  use t, tc
  apply top_unique s

theorem lindelofSpace_of_countable_subfamily_closed
    (h : âˆ€ {Î¹ : Type u} (t : Î¹ â†’ Set X), (âˆ€ i, IsClosed (t i)) â†’ â‹‚ i, t i = âˆ… â†’
      âˆƒ u : Set Î¹, u.Countable âˆ§ â‹‚ i âˆˆ u, t i = âˆ…) :
    LindelofSpace X where
  isLindelof_univ := isLindelof_of_countable_subfamily_closed fun t => by simpa using h t

theorem IsClosed.isLindelof [LindelofSpace X] (h : IsClosed s) : IsLindelof s :=
  isLindelof_univ.of_isClosed_subset h (subset_univ _)

/-- A compact set `s` is LindelÃ¶f. -/
theorem IsCompact.isLindelof (hs : IsCompact s) :
    IsLindelof s := by tauto

/-- A Ïƒ-compact set `s` is LindelÃ¶f -/
theorem IsSigmaCompact.isLindelof (hs : IsSigmaCompact s) :
    IsLindelof s := by
  rw [IsSigmaCompact] at hs
  rcases hs with âŸ¨K, âŸ¨hc, hunivâŸ©âŸ©
  rw [â† huniv]
  have hl : âˆ€ n, IsLindelof (K n) := fun n â†¦ IsCompact.isLindelof (hc n)
  exact isLindelof_iUnion hl

/-- A compact space `X` is LindelÃ¶f. -/
instance (priority := 100) [CompactSpace X] : LindelofSpace X :=
  { isLindelof_univ := isCompact_univ.isLindelof}

/-- A sigma-compact space `X` is LindelÃ¶f. -/
instance (priority := 100) [SigmaCompactSpace X] : LindelofSpace X :=
  { isLindelof_univ := isSigmaCompact_univ.isLindelof}

/-- `X` is a non-LindelÃ¶f topological space if it is not a LindelÃ¶f space. -/
class NonLindelofSpace (X : Type*) [TopologicalSpace X] : Prop where
  /-- In a non-LindelÃ¶f space, `Set.univ` is not a LindelÃ¶f set. -/
  nonLindelof_univ : Â¬IsLindelof (univ : Set X)

lemma nonLindelof_univ (X : Type*) [TopologicalSpace X] [NonLindelofSpace X] :
    Â¬IsLindelof (univ : Set X) :=
  NonLindelofSpace.nonLindelof_univ

theorem IsLindelof.ne_univ [NonLindelofSpace X] (hs : IsLindelof s) : s â‰  univ := fun h â†¦
  nonLindelof_univ X (h â–¸ hs)

instance [NonLindelofSpace X] : NeBot (Filter.coLindelof X) := by
  refine hasBasis_coLindelof.neBot_iff.2 fun {s} hs => ?_
  contrapose hs
  rw [not_nonempty_iff_eq_empty, compl_empty_iff] at hs
  rw [hs]
  exact nonLindelof_univ X

@[simp]
theorem Filter.coLindelof_eq_bot [LindelofSpace X] : Filter.coLindelof X = âŠ¥ :=
  hasBasis_coLindelof.eq_bot_iff.mpr âŸ¨Set.univ, isLindelof_univ, Set.compl_univâŸ©

instance [NonLindelofSpace X] : NeBot (Filter.coclosedLindelof X) :=
  neBot_of_le coLindelof_le_coclosedLindelof

theorem nonLindelofSpace_of_neBot (_ : NeBot (Filter.coLindelof X)) : NonLindelofSpace X :=
  âŸ¨fun h' => (Filter.nonempty_of_mem h'.compl_mem_coLindelof).ne_empty compl_univâŸ©

theorem Filter.coLindelof_neBot_iff : NeBot (Filter.coLindelof X) â†” NonLindelofSpace X :=
  âŸ¨nonLindelofSpace_of_neBot, fun _ => inferInstanceâŸ©


theorem not_LindelofSpace_iff : Â¬LindelofSpace X â†” NonLindelofSpace X :=
  âŸ¨fun hâ‚ => âŸ¨fun hâ‚‚ => hâ‚ âŸ¨hâ‚‚âŸ©âŸ©, fun âŸ¨hâ‚âŸ© âŸ¨hâ‚‚âŸ© => hâ‚ hâ‚‚âŸ©

theorem countable_of_Lindelof_of_discrete [LindelofSpace X] [DiscreteTopology X] : Countable X :=
  countable_univ_iff.mp isLindelof_univ.countable_of_discrete

theorem countable_cover_nhds_interior [LindelofSpace X] {U : X â†’ Set X} (hU : âˆ€ x, U x âˆˆ ğ“ x) :
    âˆƒ t : Set X, t.Countable âˆ§ â‹ƒ x âˆˆ t, interior (U x) = univ :=
  let âŸ¨t, htâŸ© := isLindelof_univ.elim_countable_subcover (fun x => interior (U x))
    (fun _ => isOpen_interior) fun x _ => mem_iUnion.2 âŸ¨x, mem_interior_iff_mem_nhds.2 (hU x)âŸ©
  âŸ¨t, âŸ¨ht.1, univ_subset_iff.1 ht.2âŸ©âŸ©

theorem countable_cover_nhds [LindelofSpace X] {U : X â†’ Set X} (hU : âˆ€ x, U x âˆˆ ğ“ x) :
    âˆƒ t : Set X, t.Countable âˆ§ â‹ƒ x âˆˆ t, U x = univ :=
  let âŸ¨t, htâŸ© := countable_cover_nhds_interior hU
  âŸ¨t, âŸ¨ht.1, univ_subset_iff.1 <| ht.2.symm.subset.trans <|
    iUnionâ‚‚_mono fun _ _ => interior_subsetâŸ©âŸ©

/-- The comap of the coLindelÃ¶f filter on `Y` by a continuous function `f : X â†’ Y` is less than or
equal to the coLindelÃ¶f filter on `X`.
This is a reformulation of the fact that images of LindelÃ¶f sets are LindelÃ¶f. -/
theorem Filter.comap_coLindelof_le {f : X â†’ Y} (hf : Continuous f) :
    (Filter.coLindelof Y).comap f â‰¤ Filter.coLindelof X := by
  rw [(hasBasis_coLindelof.comap f).le_basis_iff hasBasis_coLindelof]
  intro t ht
  refine âŸ¨f '' t, ht.image hf, ?_âŸ©
  simpa using t.subset_preimage_image f

theorem isLindelof_range [LindelofSpace X] {f : X â†’ Y} (hf : Continuous f) :
    IsLindelof (range f) := by rw [â† image_univ]; exact isLindelof_univ.image hf

theorem isLindelof_diagonal [LindelofSpace X] : IsLindelof (diagonal X) :=
  @range_diag X â–¸ isLindelof_range (continuous_id.prodMk continuous_id)

/-- If `f : X â†’ Y` is an inducing map, the image `f '' s` of a set `s` is LindelÃ¶f
  if and only if `s` is compact. -/
theorem Topology.IsInducing.isLindelof_iff {f : X â†’ Y} (hf : IsInducing f) :
    IsLindelof s â†” IsLindelof (f '' s) := by
  refine âŸ¨fun hs => hs.image hf.continuous, fun hs F F_ne_bot _ F_le => ?_âŸ©
  obtain âŸ¨_, âŸ¨x, x_in : x âˆˆ s, rflâŸ©, hx : ClusterPt (f x) (map f F)âŸ© :=
    hs ((map_mono F_le).trans_eq map_principal)
  exact âŸ¨x, x_in, hf.mapClusterPt_iff.1 hxâŸ©

/-- If `f : X â†’ Y` is an embedding, the image `f '' s` of a set `s` is LindelÃ¶f
if and only if `s` is LindelÃ¶f. -/
theorem Topology.IsEmbedding.isLindelof_iff {f : X â†’ Y} (hf : IsEmbedding f) :
    IsLindelof s â†” IsLindelof (f '' s) := hf.isInducing.isLindelof_iff

/-- The preimage of a LindelÃ¶f set under an inducing map is a LindelÃ¶f set. -/
theorem Topology.IsInducing.isLindelof_preimage {f : X â†’ Y} (hf : IsInducing f)
    (hf' : IsClosed (range f)) {K : Set Y} (hK : IsLindelof K) : IsLindelof (f â»Â¹' K) := by
  replace hK := hK.inter_right hf'
  rwa [hf.isLindelof_iff, image_preimage_eq_inter_range]

/-- The preimage of a LindelÃ¶f set under a closed embedding is a LindelÃ¶f set. -/
theorem Topology.IsClosedEmbedding.isLindelof_preimage {f : X â†’ Y} (hf : IsClosedEmbedding f)
    {K : Set Y} (hK : IsLindelof K) : IsLindelof (f â»Â¹' K) :=
  hf.isInducing.isLindelof_preimage (hf.isClosed_range) hK

/-- A closed embedding is proper, i.e., inverse images of LindelÃ¶f sets are contained in LindelÃ¶f.
Moreover, the preimage of a LindelÃ¶f set is LindelÃ¶f, see
`Topology.IsClosedEmbedding.isLindelof_preimage`. -/
theorem Topology.IsClosedEmbedding.tendsto_coLindelof {f : X â†’ Y} (hf : IsClosedEmbedding f) :
    Tendsto f (Filter.coLindelof X) (Filter.coLindelof Y) :=
  hasBasis_coLindelof.tendsto_right_iff.mpr fun _K hK =>
    (hf.isLindelof_preimage hK).compl_mem_coLindelof

/-- Sets of subtype are LindelÃ¶f iff the image under a coercion is. -/
theorem Subtype.isLindelof_iff {p : X â†’ Prop} {s : Set { x // p x }} :
    IsLindelof s â†” IsLindelof ((â†‘) '' s : Set X) :=
  IsEmbedding.subtypeVal.isLindelof_iff

theorem isLindelof_iff_isLindelof_univ : IsLindelof s â†” IsLindelof (univ : Set s) := by
  rw [Subtype.isLindelof_iff, image_univ, Subtype.range_coe]

theorem isLindelof_iff_LindelofSpace : IsLindelof s â†” LindelofSpace s :=
  isLindelof_iff_isLindelof_univ.trans isLindelof_univ_iff

lemma IsLindelof.of_coe [LindelofSpace s] : IsLindelof s := isLindelof_iff_LindelofSpace.mpr â€¹_â€º

theorem IsLindelof.countable (hs : IsLindelof s) (hs' : DiscreteTopology s) : s.Countable :=
  countable_coe_iff.mp
  (@countable_of_Lindelof_of_discrete _ _ (isLindelof_iff_LindelofSpace.mp hs) hs')

protected theorem Topology.IsClosedEmbedding.nonLindelofSpace [NonLindelofSpace X] {f : X â†’ Y}
    (hf : IsClosedEmbedding f) : NonLindelofSpace Y :=
  nonLindelofSpace_of_neBot hf.tendsto_coLindelof.neBot

protected theorem Topology.IsClosedEmbedding.LindelofSpace [h : LindelofSpace Y] {f : X â†’ Y}
    (hf : IsClosedEmbedding f) : LindelofSpace X :=
  âŸ¨by rw [hf.isInducing.isLindelof_iff, image_univ]; exact hf.isClosed_range.isLindelofâŸ©

/-- Countable topological spaces are Lindelof. -/
instance (priority := 100) Countable.LindelofSpace [Countable X] : LindelofSpace X where
  isLindelof_univ := countable_univ.isLindelof

/-- The disjoint union of two LindelÃ¶f spaces is LindelÃ¶f. -/
instance [LindelofSpace X] [LindelofSpace Y] : LindelofSpace (X âŠ• Y) where
  isLindelof_univ := by
    rw [â† range_inl_union_range_inr]
    exact (isLindelof_range continuous_inl).union (isLindelof_range continuous_inr)

instance {X : Î¹ â†’ Type*} [Countable Î¹] [âˆ€ i, TopologicalSpace (X i)] [âˆ€ i, LindelofSpace (X i)] :
    LindelofSpace (Î£ i, X i) where
  isLindelof_univ := by
    rw [Sigma.univ]
    exact isLindelof_iUnion fun i => isLindelof_range continuous_sigmaMk

instance Quot.LindelofSpace {r : X â†’ X â†’ Prop} [LindelofSpace X] : LindelofSpace (Quot r) where
  isLindelof_univ := by
    rw [â† range_quot_mk]
    exact isLindelof_range continuous_quot_mk

instance Quotient.LindelofSpace {s : Setoid X} [LindelofSpace X] : LindelofSpace (Quotient s) :=
  Quot.LindelofSpace

/-- A continuous image of a LindelÃ¶f set is a LindelÃ¶f set within the codomain. -/
theorem LindelofSpace.of_continuous_surjective {f : X â†’ Y} [LindelofSpace X] (hf : Continuous f)
    (hsur : Function.Surjective f) : LindelofSpace Y where
  isLindelof_univ := by
    rw [â† Set.image_univ_of_surjective hsur]
    exact IsLindelof.image (isLindelof_univ_iff.mpr â€¹_â€º) hf

/-- A set `s` is Hereditarily LindelÃ¶f if every subset is a Lindelof set. We require this only
for open sets in the definition, and then conclude that this holds for all sets by ADD. -/
def IsHereditarilyLindelof (s : Set X) :=
  âˆ€ t âŠ† s, IsLindelof t

/-- Type class for Hereditarily LindelÃ¶f spaces. -/
class HereditarilyLindelofSpace (X : Type*) [TopologicalSpace X] : Prop where
  /-- In a Hereditarily LindelÃ¶f space, `Set.univ` is a Hereditarily LindelÃ¶f set. -/
  isHereditarilyLindelof_univ : IsHereditarilyLindelof (univ : Set X)

lemma IsHereditarilyLindelof.isLindelof_subset (hs : IsHereditarilyLindelof s) (ht : t âŠ† s) :
    IsLindelof t := hs t ht

lemma IsHereditarilyLindelof.isLindelof (hs : IsHereditarilyLindelof s) :
    IsLindelof s := hs.isLindelof_subset Subset.rfl

instance (priority := 100) HereditarilyLindelof.to_Lindelof [HereditarilyLindelofSpace X] :
    LindelofSpace X where
  isLindelof_univ := HereditarilyLindelofSpace.isHereditarilyLindelof_univ.isLindelof

theorem HereditarilyLindelof_LindelofSets [HereditarilyLindelofSpace X] (s : Set X) :
    IsLindelof s := by
  apply HereditarilyLindelofSpace.isHereditarilyLindelof_univ
  exact subset_univ s

instance (priority := 100) SecondCountableTopology.toHereditarilyLindelof
    [SecondCountableTopology X] : HereditarilyLindelofSpace X where
  isHereditarilyLindelof_univ t _ _ := by
    apply isLindelof_iff_countable_subcover.mpr
    intro Î¹ U hÎ¹ hcover
    have := @isOpen_iUnion_countable X _ _ Î¹ U hÎ¹
    rcases this with âŸ¨t, âŸ¨htc, htuâŸ©âŸ©
    use t, htc
    exact subset_of_subset_of_eq hcover (id htu.symm)

lemma eq_open_union_countable [HereditarilyLindelofSpace X] {Î¹ : Type u} (U : Î¹ â†’ Set X)
    (h : âˆ€ i, IsOpen (U i)) : âˆƒ t : Set Î¹, t.Countable âˆ§ â‹ƒ iâˆˆt, U i = â‹ƒ i, U i := by
  have : IsLindelof (â‹ƒ i, U i) := HereditarilyLindelof_LindelofSets (â‹ƒ i, U i)
  rcases isLindelof_iff_countable_subcover.mp this U h (Eq.subset rfl) with âŸ¨t, âŸ¨htc, htuâŸ©âŸ©
  use t, htc
  apply eq_of_subset_of_subset (iUnionâ‚‚_subset_iUnion (fun i â†¦ i âˆˆ t) fun i â†¦ U i) htu

instance HereditarilyLindelof.lindelofSpace_subtype [HereditarilyLindelofSpace X] (p : X â†’ Prop) :
    LindelofSpace {x // p x} := by
  apply isLindelof_iff_LindelofSpace.mp
  exact HereditarilyLindelof_LindelofSets fun x â†¦ p x

end Lindelof
