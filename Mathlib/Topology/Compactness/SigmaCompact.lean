/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Yury Kudryashov
-/
import Mathlib.Topology.Bases
import Mathlib.Topology.Compactness.LocallyCompact
import Mathlib.Topology.Compactness.LocallyFinite

/-!
# Sigma-compactness in topological spaces

## Main definitions
* `IsSigmaCompact`: a set that is the union of countably many compact sets.
* `SigmaCompactSpace X`: `X` is a Ïƒ-compact topological space; i.e., is the union
  of a countable collection of compact subspaces.

-/

open Set Filter Topology TopologicalSpace

universe u v

variable {X : Type*} {Y : Type*} {Î¹ : Type*}
variable [TopologicalSpace X] [TopologicalSpace Y] {s t : Set X}

/-- A subset `s âŠ† X` is called **Ïƒ-compact** if it is the union of countably many compact sets. -/
def IsSigmaCompact (s : Set X) : Prop :=
  âˆƒ K : â„• â†’ Set X, (âˆ€ n, IsCompact (K n)) âˆ§ â‹ƒ n, K n = s

/-- Compact sets are Ïƒ-compact. -/
lemma IsCompact.isSigmaCompact {s : Set X} (hs : IsCompact s) : IsSigmaCompact s :=
  âŸ¨fun _ â†¦ s, fun _ â†¦ hs, iUnion_const _âŸ©

/-- The empty set is Ïƒ-compact. -/
@[simp]
lemma isSigmaCompact_empty : IsSigmaCompact (âˆ… : Set X) :=
  IsCompact.isSigmaCompact isCompact_empty

/-- Countable unions of compact sets are Ïƒ-compact. -/
lemma isSigmaCompact_iUnion_of_isCompact [hÎ¹ : Countable Î¹] (s : Î¹ â†’ Set X)
    (hcomp : âˆ€ i, IsCompact (s i)) : IsSigmaCompact (â‹ƒ i, s i) := by
  rcases isEmpty_or_nonempty Î¹
  Â· simp only [iUnion_of_empty, isSigmaCompact_empty]
  Â· -- If Î¹ is non-empty, choose a surjection f : â„• â†’ Î¹, this yields a map â„• â†’ Set X.
    obtain âŸ¨f, hfâŸ© := countable_iff_exists_surjective.mp hÎ¹
    exact âŸ¨s âˆ˜ f, fun n â†¦ hcomp (f n), Function.Surjective.iUnion_comp hf _âŸ©

/-- Countable unions of compact sets are Ïƒ-compact. -/
lemma isSigmaCompact_sUnion_of_isCompact {S : Set (Set X)} (hc : Set.Countable S)
    (hcomp : âˆ€ (s : Set X), s âˆˆ S â†’ IsCompact s) : IsSigmaCompact (â‹ƒâ‚€ S) := by
  have : Countable S := countable_coe_iff.mpr hc
  rw [sUnion_eq_iUnion]
  apply isSigmaCompact_iUnion_of_isCompact _ (fun âŸ¨s, hsâŸ© â†¦ hcomp s hs)

/-- Countable unions of Ïƒ-compact sets are Ïƒ-compact. -/
lemma isSigmaCompact_iUnion [Countable Î¹] (s : Î¹ â†’ Set X)
    (hcomp : âˆ€ i, IsSigmaCompact (s i)) : IsSigmaCompact (â‹ƒ i, s i) := by
  -- Choose a decomposition s_i = â‹ƒ K_i,j for each i.
  choose K hcomp hcov using fun i â†¦ hcomp i
  -- Then, we have a countable union of countable unions of compact sets, i.e. countably many.
  have := calc
    â‹ƒ i, s i
    _ = â‹ƒ i, â‹ƒ n, (K i n) := by simp_rw [hcov]
    _ = â‹ƒ (i) (n : â„•), (K.uncurry âŸ¨i, nâŸ©) := by rw [Function.uncurry_def]
    _ = â‹ƒ x, K.uncurry x := by rw [â† iUnion_prod']
  rw [this]
  exact isSigmaCompact_iUnion_of_isCompact K.uncurry fun x â†¦ (hcomp x.1 x.2)

/-- Countable unions of Ïƒ-compact sets are Ïƒ-compact. -/
lemma isSigmaCompact_sUnion (S : Set (Set X)) (hc : Set.Countable S)
    (hcomp : âˆ€ s : S, IsSigmaCompact s (X := X)) : IsSigmaCompact (â‹ƒâ‚€ S) := by
  have : Countable S := countable_coe_iff.mpr hc
  apply sUnion_eq_iUnion.symm â–¸ isSigmaCompact_iUnion _ hcomp

/-- Countable unions of Ïƒ-compact sets are Ïƒ-compact. -/
lemma isSigmaCompact_biUnion {s : Set Î¹} {S : Î¹ â†’ Set X} (hc : Set.Countable s)
    (hcomp : âˆ€ (i : Î¹), i âˆˆ s â†’ IsSigmaCompact (S i)) :
    IsSigmaCompact (â‹ƒ (i : Î¹) (_ : i âˆˆ s), S i) := by
  have : Countable â†‘s := countable_coe_iff.mpr hc
  rw [biUnion_eq_iUnion]
  exact isSigmaCompact_iUnion _ (fun âŸ¨i', hi'âŸ© â†¦ hcomp i' hi')

/-- A closed subset of a Ïƒ-compact set is Ïƒ-compact. -/
lemma IsSigmaCompact.of_isClosed_subset {s t : Set X} (ht : IsSigmaCompact t)
    (hs : IsClosed s) (h : s âŠ† t) : IsSigmaCompact s := by
  rcases ht with âŸ¨K, hcompact, hcovâŸ©
  refine âŸ¨(fun n â†¦ s âˆ© (K n)), fun n â†¦ (hcompact n).inter_left hs, ?_âŸ©
  rw [â† inter_iUnion, hcov]
  exact inter_eq_left.mpr h

/-- If `s` is Ïƒ-compact and `f` is continuous on `s`, `f(s)` is Ïƒ-compact. -/
lemma IsSigmaCompact.image_of_continuousOn {f : X â†’ Y} {s : Set X} (hs : IsSigmaCompact s)
    (hf : ContinuousOn f s) : IsSigmaCompact (f '' s) := by
  rcases hs with âŸ¨K, hcompact, hcovâŸ©
  refine âŸ¨fun n â†¦ f '' K n, ?_, hcov.symm â–¸ image_iUnion.symmâŸ©
  exact fun n â†¦ (hcompact n).image_of_continuousOn (hf.mono (hcov.symm â–¸ subset_iUnion K n))

/-- If `s` is Ïƒ-compact and `f` continuous, `f(s)` is Ïƒ-compact. -/
lemma IsSigmaCompact.image {f : X â†’ Y} (hf : Continuous f) {s : Set X} (hs : IsSigmaCompact s) :
    IsSigmaCompact (f '' s) := hs.image_of_continuousOn hf.continuousOn

/-- If `f : X â†’ Y` is an inducing map, the image `f '' s` of a set `s` is Ïƒ-compact
  if and only `s` is Ïƒ-compact. -/
lemma Topology.IsInducing.isSigmaCompact_iff {f : X â†’ Y} {s : Set X}
    (hf : IsInducing f) : IsSigmaCompact s â†” IsSigmaCompact (f '' s) := by
  constructor
  Â· exact fun h â†¦ h.image hf.continuous
  Â· rintro âŸ¨L, hcomp, hcovâŸ©
    -- Suppose f(s) is Ïƒ-compact; we want to show s is Ïƒ-compact.
    -- Write f(s) as a union of compact sets L n, so s = â‹ƒ K n with K n := fâ»Â¹(L n) âˆ© s.
    -- Since f is inducing, each K n is compact iff L n is.
    refine âŸ¨fun n â†¦ f â»Â¹' (L n) âˆ© s, ?_, ?_âŸ©
    Â· intro n
      have : f '' (f â»Â¹' (L n) âˆ© s) = L n := by
        rw [image_preimage_inter, inter_eq_left.mpr]
        exact (subset_iUnion _ n).trans hcov.le
      apply hf.isCompact_iff.mpr (this.symm â–¸ (hcomp n))
    Â· calc â‹ƒ n, f â»Â¹' L n âˆ© s
        _ = f â»Â¹' (â‹ƒ n, L n) âˆ© s  := by rw [preimage_iUnion, iUnion_inter]
        _ = f â»Â¹' (f '' s) âˆ© s := by rw [hcov]
        _ = s := inter_eq_right.mpr (subset_preimage_image _ _)

/-- If `f : X â†’ Y` is an embedding, the image `f '' s` of a set `s` is Ïƒ-compact
if and only `s` is Ïƒ-compact. -/
lemma Topology.IsEmbedding.isSigmaCompact_iff {f : X â†’ Y} {s : Set X}
    (hf : IsEmbedding f) : IsSigmaCompact s â†” IsSigmaCompact (f '' s) :=
  hf.isInducing.isSigmaCompact_iff

/-- Sets of subtype are Ïƒ-compact iff the image under a coercion is. -/
lemma Subtype.isSigmaCompact_iff {p : X â†’ Prop} {s : Set { a // p a }} :
    IsSigmaCompact s â†” IsSigmaCompact ((â†‘) '' s : Set X) :=
  IsEmbedding.subtypeVal.isSigmaCompact_iff

/-- A Ïƒ-compact space is a space that is the union of a countable collection of compact subspaces.
  Note that a locally compact separable Tâ‚‚ space need not be Ïƒ-compact.
  The sequence can be extracted using `compactCovering`. -/
class SigmaCompactSpace (X : Type*) [TopologicalSpace X] : Prop where
  /-- In a Ïƒ-compact space, `Set.univ` is a Ïƒ-compact set. -/
  isSigmaCompact_univ : IsSigmaCompact (univ : Set X)

/-- A topological space is Ïƒ-compact iff `univ` is Ïƒ-compact. -/
lemma isSigmaCompact_univ_iff : IsSigmaCompact (univ : Set X) â†” SigmaCompactSpace X :=
  âŸ¨fun h â†¦ âŸ¨hâŸ©, fun h â†¦ h.1âŸ©

/-- In a Ïƒ-compact space, `univ` is Ïƒ-compact. -/
lemma isSigmaCompact_univ [h : SigmaCompactSpace X] : IsSigmaCompact (univ : Set X) :=
  isSigmaCompact_univ_iff.mpr h

/-- A topological space is Ïƒ-compact iff there exists a countable collection of compact
subspaces that cover the entire space. -/
lemma SigmaCompactSpace_iff_exists_compact_covering :
    SigmaCompactSpace X â†” âˆƒ K : â„• â†’ Set X, (âˆ€ n, IsCompact (K n)) âˆ§ â‹ƒ n, K n = univ := by
  rw [â† isSigmaCompact_univ_iff, IsSigmaCompact]

lemma SigmaCompactSpace.exists_compact_covering [h : SigmaCompactSpace X] :
    âˆƒ K : â„• â†’ Set X, (âˆ€ n, IsCompact (K n)) âˆ§ â‹ƒ n, K n = univ :=
  SigmaCompactSpace_iff_exists_compact_covering.mp h

/-- If `X` is Ïƒ-compact, `im f` is Ïƒ-compact. -/
lemma isSigmaCompact_range {f : X â†’ Y} (hf : Continuous f) [SigmaCompactSpace X] :
    IsSigmaCompact (range f) :=
  image_univ â–¸ isSigmaCompact_univ.image hf

/-- A subset `s` is Ïƒ-compact iff `s` (with the subspace topology) is a Ïƒ-compact space. -/
lemma isSigmaCompact_iff_isSigmaCompact_univ {s : Set X} :
    IsSigmaCompact s â†” IsSigmaCompact (univ : Set s) := by
  rw [Subtype.isSigmaCompact_iff, image_univ, Subtype.range_coe]

lemma isSigmaCompact_iff_sigmaCompactSpace {s : Set X} :
    IsSigmaCompact s â†” SigmaCompactSpace s :=
  isSigmaCompact_iff_isSigmaCompact_univ.trans isSigmaCompact_univ_iff

-- see Note [lower instance priority]
instance (priority := 200) CompactSpace.sigmaCompact [CompactSpace X] : SigmaCompactSpace X :=
  âŸ¨âŸ¨fun _ â†¦ univ, fun _ â†¦ isCompact_univ, iUnion_const _âŸ©âŸ©

theorem SigmaCompactSpace.of_countable (S : Set (Set X)) (Hc : S.Countable)
    (Hcomp : âˆ€ s âˆˆ S, IsCompact s) (HU : â‹ƒâ‚€ S = univ) : SigmaCompactSpace X :=
  âŸ¨(exists_seq_cover_iff_countable âŸ¨_, isCompact_emptyâŸ©).2 âŸ¨S, Hc, Hcomp, HUâŸ©âŸ©

-- see Note [lower instance priority]
instance (priority := 100) sigmaCompactSpace_of_locallyCompact_secondCountable
    [LocallyCompactSpace X] [SecondCountableTopology X] : SigmaCompactSpace X := by
  choose K hKc hxK using fun x : X â†¦ exists_compact_mem_nhds x
  rcases countable_cover_nhds hxK with âŸ¨s, hsc, hsUâŸ©
  refine SigmaCompactSpace.of_countable _ (hsc.image K) (forall_mem_image.2 fun x _ â†¦ hKc x) ?_
  rwa [sUnion_image]

section

variable (X)
variable [SigmaCompactSpace X]

open SigmaCompactSpace

/-- A choice of compact covering for a `Ïƒ`-compact space, chosen to be monotone. -/
def compactCovering : â„• â†’ Set X :=
  Accumulate exists_compact_covering.choose

theorem isCompact_compactCovering (n : â„•) : IsCompact (compactCovering X n) :=
  isCompact_accumulate (Classical.choose_spec SigmaCompactSpace.exists_compact_covering).1 n

theorem iUnion_compactCovering : â‹ƒ n, compactCovering X n = univ := by
  rw [compactCovering, iUnion_accumulate]
  exact (Classical.choose_spec SigmaCompactSpace.exists_compact_covering).2

theorem iUnion_closure_compactCovering : â‹ƒ n, closure (compactCovering X n) = univ :=
  eq_top_mono (iUnion_mono fun _ â†¦ subset_closure) (iUnion_compactCovering X)

@[mono, gcongr]
theorem compactCovering_subset â¦ƒm n : â„•â¦„ (h : m â‰¤ n) : compactCovering X m âŠ† compactCovering X n :=
  monotone_accumulate h

variable {X}

theorem exists_mem_compactCovering (x : X) : âˆƒ n, x âˆˆ compactCovering X n :=
  iUnion_eq_univ_iff.mp (iUnion_compactCovering X) x

instance [SigmaCompactSpace Y] : SigmaCompactSpace (X Ã— Y) :=
  âŸ¨âŸ¨fun n â†¦ compactCovering X n Ã—Ë¢ compactCovering Y n, fun _ â†¦
      (isCompact_compactCovering _ _).prod (isCompact_compactCovering _ _), by
      simp only [iUnion_prod_of_monotone (compactCovering_subset X) (compactCovering_subset Y),
        iUnion_compactCovering, univ_prod_univ]âŸ©âŸ©

instance [Finite Î¹] {X : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (X i)] [âˆ€ i, SigmaCompactSpace (X i)] :
    SigmaCompactSpace (âˆ€ i, X i) := by
  refine âŸ¨âŸ¨fun n â†¦ Set.pi univ fun i â†¦ compactCovering (X i) n,
    fun n â†¦ isCompact_univ_pi fun i â†¦ isCompact_compactCovering (X i) _, ?_âŸ©âŸ©
  rw [iUnion_univ_pi_of_monotone]
  Â· simp only [iUnion_compactCovering, pi_univ]
  Â· exact fun i â†¦ compactCovering_subset (X i)

instance [SigmaCompactSpace Y] : SigmaCompactSpace (X âŠ• Y) :=
  âŸ¨âŸ¨fun n â†¦ Sum.inl '' compactCovering X n âˆª Sum.inr '' compactCovering Y n, fun n â†¦
      ((isCompact_compactCovering X n).image continuous_inl).union
        ((isCompact_compactCovering Y n).image continuous_inr),
      by simp only [iUnion_union_distrib, â† image_iUnion, iUnion_compactCovering, image_univ,
        range_inl_union_range_inr]âŸ©âŸ©

instance [Countable Î¹] {X : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (X i)]
    [âˆ€ i, SigmaCompactSpace (X i)] : SigmaCompactSpace (Î£ i, X i) := by
  cases isEmpty_or_nonempty Î¹
  Â· infer_instance
  Â· rcases exists_surjective_nat Î¹ with âŸ¨f, hfâŸ©
    refine âŸ¨âŸ¨fun n â†¦ â‹ƒ k â‰¤ n, Sigma.mk (f k) '' compactCovering (X (f k)) n, fun n â†¦ ?_, ?_âŸ©âŸ©
    Â· refine (finite_le_nat _).isCompact_biUnion fun k _ â†¦ ?_
      exact (isCompact_compactCovering _ _).image continuous_sigmaMk
    Â· simp only [iUnion_eq_univ_iff, Sigma.forall, mem_iUnion, hf.forall]
      intro k y
      rcases exists_mem_compactCovering y with âŸ¨n, hnâŸ©
      refine âŸ¨max k n, k, le_max_left _ _, mem_image_of_mem _ ?_âŸ©
      exact compactCovering_subset _ (le_max_right _ _) hn

protected lemma Topology.IsClosedEmbedding.sigmaCompactSpace {e : Y â†’ X}
    (he : IsClosedEmbedding e) : SigmaCompactSpace Y :=
  âŸ¨âŸ¨fun n â†¦ e â»Â¹' compactCovering X n, fun _ â†¦
      he.isCompact_preimage (isCompact_compactCovering _ _), by
      rw [â† preimage_iUnion, iUnion_compactCovering, preimage_univ]âŸ©âŸ©

theorem IsClosed.sigmaCompactSpace {s : Set X} (hs : IsClosed s) : SigmaCompactSpace s :=
  hs.isClosedEmbedding_subtypeVal.sigmaCompactSpace

instance [SigmaCompactSpace Y] : SigmaCompactSpace (ULift.{u} Y) :=
  IsClosedEmbedding.uliftDown.sigmaCompactSpace

/-- If `X` is a `Ïƒ`-compact space, then a locally finite family of nonempty sets of `X` can have
only countably many elements, `Set.Countable` version. -/
protected theorem LocallyFinite.countable_univ {f : Î¹ â†’ Set X} (hf : LocallyFinite f)
    (hne : âˆ€ i, (f i).Nonempty) : (univ : Set Î¹).Countable := by
  have := fun n â†¦ hf.finite_nonempty_inter_compact (isCompact_compactCovering X n)
  refine (countable_iUnion fun n â†¦ (this n).countable).mono fun i _ â†¦ ?_
  rcases hne i with âŸ¨x, hxâŸ©
  rcases iUnion_eq_univ_iff.1 (iUnion_compactCovering X) x with âŸ¨n, hnâŸ©
  exact mem_iUnion.2 âŸ¨n, x, hx, hnâŸ©

/-- If `f : Î¹ â†’ Set X` is a locally finite covering of a Ïƒ-compact topological space by nonempty
sets, then the index type `Î¹` is encodable. -/
protected noncomputable def LocallyFinite.encodable {Î¹ : Type*} {f : Î¹ â†’ Set X}
    (hf : LocallyFinite f) (hne : âˆ€ i, (f i).Nonempty) : Encodable Î¹ :=
  @Encodable.ofEquiv _ _ (hf.countable_univ hne).toEncodable (Equiv.Set.univ _).symm

/-- In a topological space with sigma compact topology, if `f` is a function that sends each point
`x` of a closed set `s` to a neighborhood of `x` within `s`, then for some countable set `t âŠ† s`,
the neighborhoods `f x`, `x âˆˆ t`, cover the whole set `s`. -/
theorem countable_cover_nhdsWithin_of_sigmaCompact {f : X â†’ Set X} {s : Set X} (hs : IsClosed s)
    (hf : âˆ€ x âˆˆ s, f x âˆˆ ð“[s] x) : âˆƒ t âŠ† s, t.Countable âˆ§ s âŠ† â‹ƒ x âˆˆ t, f x := by
  simp only [nhdsWithin, mem_inf_principal] at hf
  choose t ht hsub using fun n â†¦
    ((isCompact_compactCovering X n).inter_right hs).elim_nhds_subcover _ fun x hx â†¦ hf x hx.right
  refine
    âŸ¨â‹ƒ n, (t n : Set X), iUnion_subset fun n x hx â†¦ (ht n x hx).2,
      countable_iUnion fun n â†¦ (t n).countable_toSet, fun x hx â†¦ mem_iUnionâ‚‚.2 ?_âŸ©
  rcases exists_mem_compactCovering x with âŸ¨n, hnâŸ©
  rcases mem_iUnionâ‚‚.1 (hsub n âŸ¨hn, hxâŸ©) with âŸ¨y, hyt : y âˆˆ t n, hyf : x âˆˆ s â†’ x âˆˆ f yâŸ©
  exact âŸ¨y, mem_iUnion.2 âŸ¨n, hytâŸ©, hyf hxâŸ©

/-- In a topological space with sigma compact topology, if `f` is a function that sends each
point `x` to a neighborhood of `x`, then for some countable set `s`, the neighborhoods `f x`,
`x âˆˆ s`, cover the whole space. -/
theorem countable_cover_nhds_of_sigmaCompact {f : X â†’ Set X} (hf : âˆ€ x, f x âˆˆ ð“ x) :
    âˆƒ s : Set X, s.Countable âˆ§ â‹ƒ x âˆˆ s, f x = univ := by
  simp only [â† nhdsWithin_univ] at hf
  rcases countable_cover_nhdsWithin_of_sigmaCompact isClosed_univ fun x _ â†¦ hf x with
    âŸ¨s, -, hsc, hsUâŸ©
  exact âŸ¨s, hsc, univ_subset_iff.1 hsUâŸ©
end

/-- An [exhaustion by compact sets](https://en.wikipedia.org/wiki/Exhaustion_by_compact_sets) of a
topological space is a sequence of compact sets `K n` such that `K n âŠ† interior (K (n + 1))` and
`â‹ƒ n, K n = univ`.

If `X` is a locally compact sigma compact space, then `CompactExhaustion.choice X` provides
a choice of an exhaustion by compact sets. This choice is also available as
`(default : CompactExhaustion X)`. -/
structure CompactExhaustion (X : Type*) [TopologicalSpace X] where
  /-- The sequence of compact sets that form a compact exhaustion. -/
  toFun : â„• â†’ Set X
  /-- The sets in the compact exhaustion are in fact compact. -/
  isCompact' : âˆ€ n, IsCompact (toFun n)
  /-- The sets in the compact exhaustion form a sequence:
  each set is contained in the interior of the next. -/
  subset_interior_succ' : âˆ€ n, toFun n âŠ† interior (toFun (n + 1))
  /-- The union of all sets in a compact exhaustion equals the entire space. -/
  iUnion_eq' : â‹ƒ n, toFun n = univ

namespace CompactExhaustion

instance : FunLike (CompactExhaustion X) â„• (Set X) where
  coe := toFun
  coe_injective' | âŸ¨_, _, _, _âŸ©, âŸ¨_, _, _, _âŸ©, rfl => rfl

instance : RelHomClass (CompactExhaustion X) LE.le HasSubset.Subset where
  map_rel f _ _ h := monotone_nat_of_le_succ
    (fun n â†¦ (f.subset_interior_succ' n).trans interior_subset) h

variable (K : CompactExhaustion X)

@[simp]
theorem toFun_eq_coe : K.toFun = K := rfl

protected theorem isCompact (n : â„•) : IsCompact (K n) :=
  K.isCompact' n

theorem subset_interior_succ (n : â„•) : K n âŠ† interior (K (n + 1)) :=
  K.subset_interior_succ' n

@[mono]
protected theorem subset â¦ƒm n : â„•â¦„ (h : m â‰¤ n) : K m âŠ† K n :=
  OrderHomClass.mono K h

theorem subset_succ (n : â„•) : K n âŠ† K (n + 1) := K.subset n.le_succ

theorem subset_interior â¦ƒm n : â„•â¦„ (h : m < n) : K m âŠ† interior (K n) :=
  Subset.trans (K.subset_interior_succ m) <| interior_mono <| K.subset h

theorem iUnion_eq : â‹ƒ n, K n = univ :=
  K.iUnion_eq'

theorem exists_mem (x : X) : âˆƒ n, x âˆˆ K n :=
  iUnion_eq_univ_iff.1 K.iUnion_eq x

theorem exists_mem_nhds (x : X) : âˆƒ n, K n âˆˆ ð“ x := by
  rcases K.exists_mem x with âŸ¨n, hnâŸ©
  exact âŸ¨n + 1, mem_interior_iff_mem_nhds.mp <| K.subset_interior_succ n hnâŸ©

/-- A compact exhaustion eventually covers any compact set. -/
theorem exists_superset_of_isCompact {s : Set X} (hs : IsCompact s) : âˆƒ n, s âŠ† K n := by
  suffices âˆƒ n, s âŠ† interior (K n) from this.imp fun _ â†¦ (Subset.trans Â· interior_subset)
  refine hs.elim_directed_cover (interior âˆ˜ K) (fun _ â†¦ isOpen_interior) ?_ ?_
  Â· intro x _
    rcases K.exists_mem x with âŸ¨k, hkâŸ©
    exact mem_iUnion.2 âŸ¨k + 1, K.subset_interior_succ _ hkâŸ©
  Â· exact Monotone.directed_le fun _ _ h â†¦ interior_mono <| K.subset h

open Classical in
/-- The minimal `n` such that `x âˆˆ K n`. -/
protected noncomputable def find (x : X) : â„• :=
  Nat.find (K.exists_mem x)

theorem mem_find (x : X) : x âˆˆ K (K.find x) := by
  classical
  exact Nat.find_spec (K.exists_mem x)

theorem mem_iff_find_le {x : X} {n : â„•} : x âˆˆ K n â†” K.find x â‰¤ n := by
  classical
  exact âŸ¨fun h â†¦ Nat.find_min' (K.exists_mem x) h, fun h â†¦ K.subset h <| K.mem_find xâŸ©

/-- Prepend the empty set to a compact exhaustion `K n`. -/
def shiftr : CompactExhaustion X where
  toFun n := Nat.casesOn n âˆ… K
  isCompact' n := Nat.casesOn n isCompact_empty K.isCompact
  subset_interior_succ' n := Nat.casesOn n (empty_subset _) K.subset_interior_succ
  iUnion_eq' := iUnion_eq_univ_iff.2 fun x â†¦ âŸ¨K.find x + 1, K.mem_find xâŸ©

@[simp]
theorem find_shiftr (x : X) : K.shiftr.find x = K.find x + 1 := by
  classical
  exact Nat.find_comp_succ _ _ (notMem_empty _)

theorem mem_diff_shiftr_find (x : X) : x âˆˆ K.shiftr (K.find x + 1) \ K.shiftr (K.find x) :=
  âŸ¨K.mem_find _,
    mt K.shiftr.mem_iff_find_le.1 <| by simp only [find_shiftr, not_le, Nat.lt_succ_self]âŸ©

/-- A choice of an
[exhaustion by compact sets](https://en.wikipedia.org/wiki/Exhaustion_by_compact_sets)
of a weakly locally compact Ïƒ-compact space. -/
noncomputable def choice (X : Type*) [TopologicalSpace X] [WeaklyLocallyCompactSpace X]
    [SigmaCompactSpace X] : CompactExhaustion X := by
  apply Classical.choice
  let K : â„• â†’ { s : Set X // IsCompact s } := fun n â†¦
    Nat.recOn n âŸ¨âˆ…, isCompact_emptyâŸ© fun n s â†¦
      âŸ¨(exists_compact_superset s.2).choose âˆª compactCovering X n,
        (exists_compact_superset s.2).choose_spec.1.union (isCompact_compactCovering _ _)âŸ©
  refine âŸ¨âŸ¨fun n â†¦ (K n).1, fun n â†¦ (K n).2, fun n â†¦ ?_, ?_âŸ©âŸ©
  Â· exact Subset.trans (exists_compact_superset (K n).2).choose_spec.2
      (interior_mono subset_union_left)
  Â· refine univ_subset_iff.1 (iUnion_compactCovering X â–¸ ?_)
    exact iUnion_mono' fun n â†¦ âŸ¨n + 1, subset_union_rightâŸ©

noncomputable instance [SigmaCompactSpace X] [WeaklyLocallyCompactSpace X] :
    Inhabited (CompactExhaustion X) :=
  âŸ¨CompactExhaustion.choice XâŸ©

end CompactExhaustion
