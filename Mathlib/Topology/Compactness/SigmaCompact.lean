/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro, Yury Kudryashov
-/
import Mathlib.Topology.Bases
import Mathlib.Topology.Compactness.LocallyCompact
import Mathlib.Topology.Compactness.LocallyFinite

/-!
# Sigma-compactness in topological spaces

## Main definitions
* `IsSigmaCompact`: a set that is the union of countably many compact sets.
* `SigmaCompactSpace X`: `X` is a œÉ-compact topological space; i.e., is the union
  of a countable collection of compact subspaces.

-/

open Set Filter Topology TopologicalSpace

universe u v

variable {X : Type*} {Y : Type*} {Œπ : Type*}
variable [TopologicalSpace X] [TopologicalSpace Y] {s t : Set X}

/-- A subset `s ‚äÜ X` is called **œÉ-compact** if it is the union of countably many compact sets. -/
def IsSigmaCompact (s : Set X) : Prop :=
  ‚àÉ K : ‚Ñï ‚Üí Set X, (‚àÄ n, IsCompact (K n)) ‚àß ‚ãÉ n, K n = s

/-- Compact sets are œÉ-compact. -/
lemma IsCompact.isSigmaCompact {s : Set X} (hs : IsCompact s) : IsSigmaCompact s :=
  ‚ü®fun _ => s, fun _ => hs, iUnion_const _‚ü©

/-- The empty set is œÉ-compact. -/
@[simp]
lemma isSigmaCompact_empty : IsSigmaCompact (‚àÖ : Set X) :=
  IsCompact.isSigmaCompact isCompact_empty

/-- Countable unions of compact sets are œÉ-compact. -/
lemma isSigmaCompact_iUnion_of_isCompact [hŒπ : Countable Œπ] (s : Œπ ‚Üí Set X)
    (hcomp : ‚àÄ i, IsCompact (s i)) : IsSigmaCompact (‚ãÉ i, s i) := by
  rcases isEmpty_or_nonempty Œπ
  ¬∑ simp only [iUnion_of_empty, isSigmaCompact_empty]
  ¬∑ -- If Œπ is non-empty, choose a surjection f : ‚Ñï ‚Üí Œπ, this yields a map ‚Ñï ‚Üí Set X.
    obtain ‚ü®f, hf‚ü© := countable_iff_exists_surjective.mp hŒπ
    exact ‚ü®s ‚àò f, fun n ‚Ü¶ hcomp (f n), Function.Surjective.iUnion_comp hf _‚ü©

/-- Countable unions of compact sets are œÉ-compact. -/
lemma isSigmaCompact_sUnion_of_isCompact {S : Set (Set X)} (hc : Set.Countable S)
    (hcomp : ‚àÄ (s : Set X), s ‚àà S ‚Üí IsCompact s) : IsSigmaCompact (‚ãÉ‚ÇÄ S) := by
  have : Countable S := countable_coe_iff.mpr hc
  rw [sUnion_eq_iUnion]
  apply isSigmaCompact_iUnion_of_isCompact _ (fun ‚ü®s, hs‚ü© ‚Ü¶ hcomp s hs)

/-- Countable unions of œÉ-compact sets are œÉ-compact. -/
lemma isSigmaCompact_iUnion [Countable Œπ] (s : Œπ ‚Üí Set X)
    (hcomp : ‚àÄ i, IsSigmaCompact (s i)) : IsSigmaCompact (‚ãÉ i, s i) := by
  -- Choose a decomposition s_i = ‚ãÉ K_i,j for each i.
  choose K hcomp hcov using fun i ‚Ü¶ hcomp i
  -- Then, we have a countable union of countable unions of compact sets, i.e. countably many.
  have := calc
    ‚ãÉ i, s i
    _ = ‚ãÉ i, ‚ãÉ n, (K i n) := by simp_rw [hcov]
    _ = ‚ãÉ (i) (n : ‚Ñï), (K.uncurry ‚ü®i, n‚ü©) := by rw [Function.uncurry_def]
    _ = ‚ãÉ x, K.uncurry x := by rw [‚Üê iUnion_prod']
  rw [this]
  exact isSigmaCompact_iUnion_of_isCompact K.uncurry fun x ‚Ü¶ (hcomp x.1 x.2)

/-- Countable unions of œÉ-compact sets are œÉ-compact. -/
lemma isSigmaCompact_sUnion (S : Set (Set X)) (hc : Set.Countable S)
    (hcomp : ‚àÄ s : S, IsSigmaCompact s (X := X)) : IsSigmaCompact (‚ãÉ‚ÇÄ S) := by
  have : Countable S := countable_coe_iff.mpr hc
  apply sUnion_eq_iUnion.symm ‚ñ∏ isSigmaCompact_iUnion _ hcomp

/-- Countable unions of œÉ-compact sets are œÉ-compact. -/
lemma isSigmaCompact_biUnion {s : Set Œπ} {S : Œπ ‚Üí Set X} (hc : Set.Countable s)
    (hcomp : ‚àÄ (i : Œπ), i ‚àà s ‚Üí IsSigmaCompact (S i)) :
    IsSigmaCompact (‚ãÉ (i : Œπ) (_ : i ‚àà s), S i) := by
  have : Countable ‚Üës := countable_coe_iff.mpr hc
  rw [biUnion_eq_iUnion]
  exact isSigmaCompact_iUnion _ (fun ‚ü®i', hi'‚ü© ‚Ü¶ hcomp i' hi')

/-- A closed subset of a œÉ-compact set is œÉ-compact. -/
lemma IsSigmaCompact.of_isClosed_subset {s t : Set X} (ht : IsSigmaCompact t)
    (hs : IsClosed s) (h : s ‚äÜ t) : IsSigmaCompact s := by
  rcases ht with ‚ü®K, hcompact, hcov‚ü©
  refine ‚ü®(fun n ‚Ü¶ s ‚à© (K n)), fun n ‚Ü¶ (hcompact n).inter_left hs, ?_‚ü©
  rw [‚Üê inter_iUnion, hcov]
  exact inter_eq_left.mpr h

/-- If `s` is œÉ-compact and `f` is continuous on `s`, `f(s)` is œÉ-compact. -/
lemma IsSigmaCompact.image_of_continuousOn {f : X ‚Üí Y} {s : Set X} (hs : IsSigmaCompact s)
    (hf : ContinuousOn f s) : IsSigmaCompact (f '' s) := by
  rcases hs with ‚ü®K, hcompact, hcov‚ü©
  refine ‚ü®fun n ‚Ü¶ f '' K n, ?_, hcov.symm ‚ñ∏ image_iUnion.symm‚ü©
  exact fun n ‚Ü¶ (hcompact n).image_of_continuousOn (hf.mono (hcov.symm ‚ñ∏ subset_iUnion K n))

/-- If `s` is œÉ-compact and `f` continuous, `f(s)` is œÉ-compact. -/
lemma IsSigmaCompact.image {f : X ‚Üí Y} (hf : Continuous f) {s : Set X} (hs : IsSigmaCompact s) :
    IsSigmaCompact (f '' s) := hs.image_of_continuousOn hf.continuousOn

/-- If `f : X ‚Üí Y` is an inducing map, the image `f '' s` of a set `s` is œÉ-compact
  if and only `s` is œÉ-compact. -/
lemma Topology.IsInducing.isSigmaCompact_iff {f : X ‚Üí Y} {s : Set X}
    (hf : IsInducing f) : IsSigmaCompact s ‚Üî IsSigmaCompact (f '' s) := by
  constructor
  ¬∑ exact fun h ‚Ü¶ h.image hf.continuous
  ¬∑ rintro ‚ü®L, hcomp, hcov‚ü©
    -- Suppose f(s) is œÉ-compact; we want to show s is œÉ-compact.
    -- Write f(s) as a union of compact sets L n, so s = ‚ãÉ K n with K n := f‚Åª¬π(L n) ‚à© s.
    -- Since f is inducing, each K n is compact iff L n is.
    refine ‚ü®fun n ‚Ü¶ f ‚Åª¬π' (L n) ‚à© s, ?_, ?_‚ü©
    ¬∑ intro n
      have : f '' (f ‚Åª¬π' (L n) ‚à© s) = L n := by
        rw [image_preimage_inter, inter_eq_left.mpr]
        exact (subset_iUnion _ n).trans hcov.le
      apply hf.isCompact_iff.mpr (this.symm ‚ñ∏ (hcomp n))
    ¬∑ calc ‚ãÉ n, f ‚Åª¬π' L n ‚à© s
        _ = f ‚Åª¬π' (‚ãÉ n, L n) ‚à© s  := by rw [preimage_iUnion, iUnion_inter]
        _ = f ‚Åª¬π' (f '' s) ‚à© s := by rw [hcov]
        _ = s := inter_eq_right.mpr (subset_preimage_image _ _)

/-- If `f : X ‚Üí Y` is an embedding, the image `f '' s` of a set `s` is œÉ-compact
if and only `s` is œÉ-compact. -/
lemma Topology.IsEmbedding.isSigmaCompact_iff {f : X ‚Üí Y} {s : Set X}
    (hf : IsEmbedding f) : IsSigmaCompact s ‚Üî IsSigmaCompact (f '' s) :=
  hf.isInducing.isSigmaCompact_iff

/-- Sets of subtype are œÉ-compact iff the image under a coercion is. -/
lemma Subtype.isSigmaCompact_iff {p : X ‚Üí Prop} {s : Set { a // p a }} :
    IsSigmaCompact s ‚Üî IsSigmaCompact ((‚Üë) '' s : Set X) :=
  IsEmbedding.subtypeVal.isSigmaCompact_iff

/-- A œÉ-compact space is a space that is the union of a countable collection of compact subspaces.
  Note that a locally compact separable T‚ÇÇ space need not be œÉ-compact.
  The sequence can be extracted using `compactCovering`. -/
class SigmaCompactSpace (X : Type*) [TopologicalSpace X] : Prop where
  /-- In a œÉ-compact space, `Set.univ` is a œÉ-compact set. -/
  isSigmaCompact_univ : IsSigmaCompact (univ : Set X)

/-- A topological space is œÉ-compact iff `univ` is œÉ-compact. -/
lemma isSigmaCompact_univ_iff : IsSigmaCompact (univ : Set X) ‚Üî SigmaCompactSpace X :=
  ‚ü®fun h => ‚ü®h‚ü©, fun h => h.1‚ü©

/-- In a œÉ-compact space, `univ` is œÉ-compact. -/
lemma isSigmaCompact_univ [h : SigmaCompactSpace X] : IsSigmaCompact (univ : Set X) :=
  isSigmaCompact_univ_iff.mpr h

/-- A topological space is œÉ-compact iff there exists a countable collection of compact
subspaces that cover the entire space. -/
lemma SigmaCompactSpace_iff_exists_compact_covering :
    SigmaCompactSpace X ‚Üî ‚àÉ K : ‚Ñï ‚Üí Set X, (‚àÄ n, IsCompact (K n)) ‚àß ‚ãÉ n, K n = univ := by
  rw [‚Üê isSigmaCompact_univ_iff, IsSigmaCompact]

lemma SigmaCompactSpace.exists_compact_covering [h : SigmaCompactSpace X] :
    ‚àÉ K : ‚Ñï ‚Üí Set X, (‚àÄ n, IsCompact (K n)) ‚àß ‚ãÉ n, K n = univ :=
  SigmaCompactSpace_iff_exists_compact_covering.mp h

/-- If `X` is œÉ-compact, `im f` is œÉ-compact. -/
lemma isSigmaCompact_range {f : X ‚Üí Y} (hf : Continuous f) [SigmaCompactSpace X] :
    IsSigmaCompact (range f) :=
  image_univ ‚ñ∏ isSigmaCompact_univ.image hf

/-- A subset `s` is œÉ-compact iff `s` (with the subspace topology) is a œÉ-compact space. -/
lemma isSigmaCompact_iff_isSigmaCompact_univ {s : Set X} :
    IsSigmaCompact s ‚Üî IsSigmaCompact (univ : Set s) := by
  rw [Subtype.isSigmaCompact_iff, image_univ, Subtype.range_coe]

lemma isSigmaCompact_iff_sigmaCompactSpace {s : Set X} :
    IsSigmaCompact s ‚Üî SigmaCompactSpace s :=
  isSigmaCompact_iff_isSigmaCompact_univ.trans isSigmaCompact_univ_iff

-- see Note [lower instance priority]
instance (priority := 200) CompactSpace.sigmaCompact [CompactSpace X] : SigmaCompactSpace X :=
  ‚ü®‚ü®fun _ => univ, fun _ => isCompact_univ, iUnion_const _‚ü©‚ü©

theorem SigmaCompactSpace.of_countable (S : Set (Set X)) (Hc : S.Countable)
    (Hcomp : ‚àÄ s ‚àà S, IsCompact s) (HU : ‚ãÉ‚ÇÄ S = univ) : SigmaCompactSpace X :=
  ‚ü®(exists_seq_cover_iff_countable ‚ü®_, isCompact_empty‚ü©).2 ‚ü®S, Hc, Hcomp, HU‚ü©‚ü©

-- see Note [lower instance priority]
instance (priority := 100) sigmaCompactSpace_of_locallyCompact_secondCountable
    [LocallyCompactSpace X] [SecondCountableTopology X] : SigmaCompactSpace X := by
  choose K hKc hxK using fun x : X => exists_compact_mem_nhds x
  rcases countable_cover_nhds hxK with ‚ü®s, hsc, hsU‚ü©
  refine SigmaCompactSpace.of_countable _ (hsc.image K) (forall_mem_image.2 fun x _ => hKc x) ?_
  rwa [sUnion_image]

section

variable (X)
variable [SigmaCompactSpace X]

open SigmaCompactSpace

/-- A choice of compact covering for a `œÉ`-compact space, chosen to be monotone. -/
def compactCovering : ‚Ñï ‚Üí Set X :=
  Accumulate exists_compact_covering.choose

theorem isCompact_compactCovering (n : ‚Ñï) : IsCompact (compactCovering X n) :=
  isCompact_accumulate (Classical.choose_spec SigmaCompactSpace.exists_compact_covering).1 n

theorem iUnion_compactCovering : ‚ãÉ n, compactCovering X n = univ := by
  rw [compactCovering, iUnion_accumulate]
  exact (Classical.choose_spec SigmaCompactSpace.exists_compact_covering).2

theorem iUnion_closure_compactCovering : ‚ãÉ n, closure (compactCovering X n) = univ :=
  eq_top_mono (iUnion_mono fun _ ‚Ü¶ subset_closure) (iUnion_compactCovering X)

@[mono, gcongr]
theorem compactCovering_subset ‚¶Ém n : ‚Ñï‚¶Ñ (h : m ‚â§ n) : compactCovering X m ‚äÜ compactCovering X n :=
  monotone_accumulate h

variable {X}

theorem exists_mem_compactCovering (x : X) : ‚àÉ n, x ‚àà compactCovering X n :=
  iUnion_eq_univ_iff.mp (iUnion_compactCovering X) x

instance [SigmaCompactSpace Y] : SigmaCompactSpace (X √ó Y) :=
  ‚ü®‚ü®fun n => compactCovering X n √óÀ¢ compactCovering Y n, fun _ =>
      (isCompact_compactCovering _ _).prod (isCompact_compactCovering _ _), by
      simp only [iUnion_prod_of_monotone (compactCovering_subset X) (compactCovering_subset Y),
        iUnion_compactCovering, univ_prod_univ]‚ü©‚ü©

instance [Finite Œπ] {X : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (X i)] [‚àÄ i, SigmaCompactSpace (X i)] :
    SigmaCompactSpace (‚àÄ i, X i) := by
  refine ‚ü®‚ü®fun n => Set.pi univ fun i => compactCovering (X i) n,
    fun n => isCompact_univ_pi fun i => isCompact_compactCovering (X i) _, ?_‚ü©‚ü©
  rw [iUnion_univ_pi_of_monotone]
  ¬∑ simp only [iUnion_compactCovering, pi_univ]
  ¬∑ exact fun i => compactCovering_subset (X i)

instance [SigmaCompactSpace Y] : SigmaCompactSpace (X ‚äï Y) :=
  ‚ü®‚ü®fun n => Sum.inl '' compactCovering X n ‚à™ Sum.inr '' compactCovering Y n, fun n =>
      ((isCompact_compactCovering X n).image continuous_inl).union
        ((isCompact_compactCovering Y n).image continuous_inr),
      by simp only [iUnion_union_distrib, ‚Üê image_iUnion, iUnion_compactCovering, image_univ,
        range_inl_union_range_inr]‚ü©‚ü©

instance [Countable Œπ] {X : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (X i)]
    [‚àÄ i, SigmaCompactSpace (X i)] : SigmaCompactSpace (Œ£ i, X i) := by
  cases isEmpty_or_nonempty Œπ
  ¬∑ infer_instance
  ¬∑ rcases exists_surjective_nat Œπ with ‚ü®f, hf‚ü©
    refine ‚ü®‚ü®fun n => ‚ãÉ k ‚â§ n, Sigma.mk (f k) '' compactCovering (X (f k)) n, fun n => ?_, ?_‚ü©‚ü©
    ¬∑ refine (finite_le_nat _).isCompact_biUnion fun k _ => ?_
      exact (isCompact_compactCovering _ _).image continuous_sigmaMk
    ¬∑ simp only [iUnion_eq_univ_iff, Sigma.forall, mem_iUnion, hf.forall]
      intro k y
      rcases exists_mem_compactCovering y with ‚ü®n, hn‚ü©
      refine ‚ü®max k n, k, le_max_left _ _, mem_image_of_mem _ ?_‚ü©
      exact compactCovering_subset _ (le_max_right _ _) hn

protected lemma Topology.IsClosedEmbedding.sigmaCompactSpace {e : Y ‚Üí X}
    (he : IsClosedEmbedding e) : SigmaCompactSpace Y :=
  ‚ü®‚ü®fun n => e ‚Åª¬π' compactCovering X n, fun _ =>
      he.isCompact_preimage (isCompact_compactCovering _ _), by
      rw [‚Üê preimage_iUnion, iUnion_compactCovering, preimage_univ]‚ü©‚ü©

theorem IsClosed.sigmaCompactSpace {s : Set X} (hs : IsClosed s) : SigmaCompactSpace s :=
  hs.isClosedEmbedding_subtypeVal.sigmaCompactSpace

instance [SigmaCompactSpace Y] : SigmaCompactSpace (ULift.{u} Y) :=
  IsClosedEmbedding.uliftDown.sigmaCompactSpace

/-- If `X` is a `œÉ`-compact space, then a locally finite family of nonempty sets of `X` can have
only countably many elements, `Set.Countable` version. -/
protected theorem LocallyFinite.countable_univ {f : Œπ ‚Üí Set X} (hf : LocallyFinite f)
    (hne : ‚àÄ i, (f i).Nonempty) : (univ : Set Œπ).Countable := by
  have := fun n => hf.finite_nonempty_inter_compact (isCompact_compactCovering X n)
  refine (countable_iUnion fun n => (this n).countable).mono fun i _ => ?_
  rcases hne i with ‚ü®x, hx‚ü©
  rcases iUnion_eq_univ_iff.1 (iUnion_compactCovering X) x with ‚ü®n, hn‚ü©
  exact mem_iUnion.2 ‚ü®n, x, hx, hn‚ü©

/-- If `f : Œπ ‚Üí Set X` is a locally finite covering of a œÉ-compact topological space by nonempty
sets, then the index type `Œπ` is encodable. -/
protected noncomputable def LocallyFinite.encodable {Œπ : Type*} {f : Œπ ‚Üí Set X}
    (hf : LocallyFinite f) (hne : ‚àÄ i, (f i).Nonempty) : Encodable Œπ :=
  @Encodable.ofEquiv _ _ (hf.countable_univ hne).toEncodable (Equiv.Set.univ _).symm

/-- In a topological space with sigma compact topology, if `f` is a function that sends each point
`x` of a closed set `s` to a neighborhood of `x` within `s`, then for some countable set `t ‚äÜ s`,
the neighborhoods `f x`, `x ‚àà t`, cover the whole set `s`. -/
theorem countable_cover_nhdsWithin_of_sigmaCompact {f : X ‚Üí Set X} {s : Set X} (hs : IsClosed s)
    (hf : ‚àÄ x ‚àà s, f x ‚àà ùìù[s] x) : ‚àÉ t ‚äÜ s, t.Countable ‚àß s ‚äÜ ‚ãÉ x ‚àà t, f x := by
  simp only [nhdsWithin, mem_inf_principal] at hf
  choose t ht hsub using fun n =>
    ((isCompact_compactCovering X n).inter_right hs).elim_nhds_subcover _ fun x hx => hf x hx.right
  refine
    ‚ü®‚ãÉ n, (t n : Set X), iUnion_subset fun n x hx => (ht n x hx).2,
      countable_iUnion fun n => (t n).countable_toSet, fun x hx => mem_iUnion‚ÇÇ.2 ?_‚ü©
  rcases exists_mem_compactCovering x with ‚ü®n, hn‚ü©
  rcases mem_iUnion‚ÇÇ.1 (hsub n ‚ü®hn, hx‚ü©) with ‚ü®y, hyt : y ‚àà t n, hyf : x ‚àà s ‚Üí x ‚àà f y‚ü©
  exact ‚ü®y, mem_iUnion.2 ‚ü®n, hyt‚ü©, hyf hx‚ü©

/-- In a topological space with sigma compact topology, if `f` is a function that sends each
point `x` to a neighborhood of `x`, then for some countable set `s`, the neighborhoods `f x`,
`x ‚àà s`, cover the whole space. -/
theorem countable_cover_nhds_of_sigmaCompact {f : X ‚Üí Set X} (hf : ‚àÄ x, f x ‚àà ùìù x) :
    ‚àÉ s : Set X, s.Countable ‚àß ‚ãÉ x ‚àà s, f x = univ := by
  simp only [‚Üê nhdsWithin_univ] at hf
  rcases countable_cover_nhdsWithin_of_sigmaCompact isClosed_univ fun x _ => hf x with
    ‚ü®s, -, hsc, hsU‚ü©
  exact ‚ü®s, hsc, univ_subset_iff.1 hsU‚ü©
end

/-- An [exhaustion by compact sets](https://en.wikipedia.org/wiki/Exhaustion_by_compact_sets) of a
topological space is a sequence of compact sets `K n` such that `K n ‚äÜ interior (K (n + 1))` and
`‚ãÉ n, K n = univ`.

If `X` is a locally compact sigma compact space, then `CompactExhaustion.choice X` provides
a choice of an exhaustion by compact sets. This choice is also available as
`(default : CompactExhaustion X)`. -/
structure CompactExhaustion (X : Type*) [TopologicalSpace X] where
  /-- The sequence of compact sets that form a compact exhaustion. -/
  toFun : ‚Ñï ‚Üí Set X
  /-- The sets in the compact exhaustion are in fact compact. -/
  isCompact' : ‚àÄ n, IsCompact (toFun n)
  /-- The sets in the compact exhaustion form a sequence:
  each set is contained in the interior of the next. -/
  subset_interior_succ' : ‚àÄ n, toFun n ‚äÜ interior (toFun (n + 1))
  /-- The union of all sets in a compact exhaustion equals the entire space. -/
  iUnion_eq' : ‚ãÉ n, toFun n = univ

namespace CompactExhaustion

instance : FunLike (CompactExhaustion X) ‚Ñï (Set X) where
  coe := toFun
  coe_injective' | ‚ü®_, _, _, _‚ü©, ‚ü®_, _, _, _‚ü©, rfl => rfl

instance : RelHomClass (CompactExhaustion X) LE.le HasSubset.Subset where
  map_rel f _ _ h := monotone_nat_of_le_succ
    (fun n ‚Ü¶ (f.subset_interior_succ' n).trans interior_subset) h

variable (K : CompactExhaustion X)

@[simp]
theorem toFun_eq_coe : K.toFun = K := rfl

protected theorem isCompact (n : ‚Ñï) : IsCompact (K n) :=
  K.isCompact' n

theorem subset_interior_succ (n : ‚Ñï) : K n ‚äÜ interior (K (n + 1)) :=
  K.subset_interior_succ' n

@[mono]
protected theorem subset ‚¶Ém n : ‚Ñï‚¶Ñ (h : m ‚â§ n) : K m ‚äÜ K n :=
  OrderHomClass.mono K h

theorem subset_succ (n : ‚Ñï) : K n ‚äÜ K (n + 1) := K.subset n.le_succ

theorem subset_interior ‚¶Ém n : ‚Ñï‚¶Ñ (h : m < n) : K m ‚äÜ interior (K n) :=
  Subset.trans (K.subset_interior_succ m) <| interior_mono <| K.subset h

theorem iUnion_eq : ‚ãÉ n, K n = univ :=
  K.iUnion_eq'

theorem exists_mem (x : X) : ‚àÉ n, x ‚àà K n :=
  iUnion_eq_univ_iff.1 K.iUnion_eq x

theorem exists_mem_nhds (x : X) : ‚àÉ n, K n ‚àà ùìù x := by
  rcases K.exists_mem x with ‚ü®n, hn‚ü©
  exact ‚ü®n + 1, mem_interior_iff_mem_nhds.mp <| K.subset_interior_succ n hn‚ü©

/-- A compact exhaustion eventually covers any compact set. -/
theorem exists_superset_of_isCompact {s : Set X} (hs : IsCompact s) : ‚àÉ n, s ‚äÜ K n := by
  suffices ‚àÉ n, s ‚äÜ interior (K n) from this.imp fun _ ‚Ü¶ (Subset.trans ¬∑ interior_subset)
  refine hs.elim_directed_cover (interior ‚àò K) (fun _ ‚Ü¶ isOpen_interior) ?_ ?_
  ¬∑ intro x _
    rcases K.exists_mem x with ‚ü®k, hk‚ü©
    exact mem_iUnion.2 ‚ü®k + 1, K.subset_interior_succ _ hk‚ü©
  ¬∑ exact Monotone.directed_le fun _ _ h ‚Ü¶ interior_mono <| K.subset h

open Classical in
/-- The minimal `n` such that `x ‚àà K n`. -/
protected noncomputable def find (x : X) : ‚Ñï :=
  Nat.find (K.exists_mem x)

theorem mem_find (x : X) : x ‚àà K (K.find x) := by
  classical
  exact Nat.find_spec (K.exists_mem x)

theorem mem_iff_find_le {x : X} {n : ‚Ñï} : x ‚àà K n ‚Üî K.find x ‚â§ n := by
  classical
  exact ‚ü®fun h => Nat.find_min' (K.exists_mem x) h, fun h => K.subset h <| K.mem_find x‚ü©

/-- Prepend the empty set to a compact exhaustion `K n`. -/
def shiftr : CompactExhaustion X where
  toFun n := Nat.casesOn n ‚àÖ K
  isCompact' n := Nat.casesOn n isCompact_empty K.isCompact
  subset_interior_succ' n := Nat.casesOn n (empty_subset _) K.subset_interior_succ
  iUnion_eq' := iUnion_eq_univ_iff.2 fun x => ‚ü®K.find x + 1, K.mem_find x‚ü©

@[simp]
theorem find_shiftr (x : X) : K.shiftr.find x = K.find x + 1 := by
  classical
  exact Nat.find_comp_succ _ _ (notMem_empty _)

theorem mem_diff_shiftr_find (x : X) : x ‚àà K.shiftr (K.find x + 1) \ K.shiftr (K.find x) :=
  ‚ü®K.mem_find _,
    mt K.shiftr.mem_iff_find_le.1 <| by simp only [find_shiftr, not_le, Nat.lt_succ_self]‚ü©

/-- A choice of an
[exhaustion by compact sets](https://en.wikipedia.org/wiki/Exhaustion_by_compact_sets)
of a weakly locally compact œÉ-compact space. -/
noncomputable def choice (X : Type*) [TopologicalSpace X] [WeaklyLocallyCompactSpace X]
    [SigmaCompactSpace X] : CompactExhaustion X := by
  apply Classical.choice
  let K : ‚Ñï ‚Üí { s : Set X // IsCompact s } := fun n =>
    Nat.recOn n ‚ü®‚àÖ, isCompact_empty‚ü© fun n s =>
      ‚ü®(exists_compact_superset s.2).choose ‚à™ compactCovering X n,
        (exists_compact_superset s.2).choose_spec.1.union (isCompact_compactCovering _ _)‚ü©
  refine ‚ü®‚ü®fun n ‚Ü¶ (K n).1, fun n => (K n).2, fun n ‚Ü¶ ?_, ?_‚ü©‚ü©
  ¬∑ exact Subset.trans (exists_compact_superset (K n).2).choose_spec.2
      (interior_mono subset_union_left)
  ¬∑ refine univ_subset_iff.1 (iUnion_compactCovering X ‚ñ∏ ?_)
    exact iUnion_mono' fun n => ‚ü®n + 1, subset_union_right‚ü©

noncomputable instance [SigmaCompactSpace X] [WeaklyLocallyCompactSpace X] :
    Inhabited (CompactExhaustion X) :=
  ‚ü®CompactExhaustion.choice X‚ü©

end CompactExhaustion
