/-
Copyright (c) 2018 Jan-David Salchow. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, Patrick Massot, Yury Kudryashov
-/
import Mathlib.Topology.Defs.Sequences
import Mathlib.Topology.UniformSpace.Cauchy

/-!
# Sequences in topological spaces

In this file we prove theorems about relations
between closure/compactness/continuity etc. and their sequential counterparts.

## Main definitions

The following notions are defined in `Topology/Defs/Sequences`.
We build theory about these definitions here, so we remind the definitions.

### Set operation
* `seqClosure s`: sequential closure of a set, the set of limits of sequences of points of `s`;

### Predicates

* `IsSeqClosed s`: predicate saying that a set is sequentially closed, i.e., `seqClosure s âŠ† s`;
* `SeqContinuous f`: predicate saying that a function is sequentially continuous, i.e.,
  for any sequence `u : â„• â†’ X` that converges to a point `x`, the sequence `f âˆ˜ u` converges to
  `f x`;
* `IsSeqCompact s`: predicate saying that a set is sequentially compact, i.e., every sequence
  taking values in `s` has a converging subsequence.

### Type classes

* `FrechetUrysohnSpace X`: a typeclass saying that a topological space is a *FrÃ©chet-Urysohn
  space*, i.e., the sequential closure of any set is equal to its closure.
* `SequentialSpace X`: a typeclass saying that a topological space is a *sequential space*, i.e.,
  any sequentially closed set in this space is closed. This condition is weaker than being a
  FrÃ©chet-Urysohn space.
* `SeqCompactSpace X`: a typeclass saying that a topological space is sequentially compact, i.e.,
  every sequence in `X` has a converging subsequence.

## Main results

* `seqClosure_subset_closure`: closure of a set includes its sequential closure;
* `IsClosed.isSeqClosed`: a closed set is sequentially closed;
* `IsSeqClosed.seqClosure_eq`: sequential closure of a sequentially closed set `s` is equal
  to `s`;
* `seqClosure_eq_closure`: in a FrÃ©chet-Urysohn space, the sequential closure of a set is equal to
  its closure;
* `tendsto_nhds_iff_seq_tendsto`, `FrechetUrysohnSpace.of_seq_tendsto_imp_tendsto`: a topological
  space is a FrÃ©chet-Urysohn space if and only if sequential convergence implies convergence;
* `FirstCountableTopology.frechetUrysohnSpace`: every topological space with
  first countable topology is a FrÃ©chet-Urysohn space;
* `FrechetUrysohnSpace.to_sequentialSpace`: every FrÃ©chet-Urysohn space is a sequential space;
* `IsSeqCompact.isCompact`: a sequentially compact set in a uniform space with countably
  generated uniformity is compact.

## Tags

sequentially closed, sequentially compact, sequential space
-/


open Bornology Filter Function Set TopologicalSpace Topology
open scoped Uniformity

variable {X Y : Type*}

/-! ### Sequential closures, sequential continuity, and sequential spaces. -/

section TopologicalSpace

variable [TopologicalSpace X] [TopologicalSpace Y]

theorem subset_seqClosure {s : Set X} : s âŠ† seqClosure s := fun p hp =>
  âŸ¨const â„• p, fun _ => hp, tendsto_const_nhdsâŸ©

/-- The sequential closure of a set is contained in the closure of that set.
The converse is not true. -/
theorem seqClosure_subset_closure {s : Set X} : seqClosure s âŠ† closure s := fun _p âŸ¨_x, xM, xpâŸ© =>
  mem_closure_of_tendsto xp (univ_mem' xM)

/-- The sequential closure of a sequentially closed set is the set itself. -/
theorem IsSeqClosed.seqClosure_eq {s : Set X} (hs : IsSeqClosed s) : seqClosure s = s :=
  Subset.antisymm (fun _p âŸ¨_x, hx, hpâŸ© => hs hx hp) subset_seqClosure

/-- If a set is equal to its sequential closure, then it is sequentially closed. -/
theorem isSeqClosed_of_seqClosure_eq {s : Set X} (hs : seqClosure s = s) : IsSeqClosed s :=
  fun x _p hxs hxp => hs â–¸ âŸ¨x, hxs, hxpâŸ©

/-- A set is sequentially closed iff it is equal to its sequential closure. -/
theorem isSeqClosed_iff {s : Set X} : IsSeqClosed s â†” seqClosure s = s :=
  âŸ¨IsSeqClosed.seqClosure_eq, isSeqClosed_of_seqClosure_eqâŸ©

/-- A set is sequentially closed if it is closed. -/
protected theorem IsClosed.isSeqClosed {s : Set X} (hc : IsClosed s) : IsSeqClosed s :=
  fun _u _x hu hx => hc.mem_of_tendsto hx (Eventually.of_forall hu)

theorem seqClosure_eq_closure [FrechetUrysohnSpace X] (s : Set X) : seqClosure s = closure s :=
  seqClosure_subset_closure.antisymm <| FrechetUrysohnSpace.closure_subset_seqClosure s

/-- In a FrÃ©chet-Urysohn space, a point belongs to the closure of a set iff it is a limit
of a sequence taking values in this set. -/
theorem mem_closure_iff_seq_limit [FrechetUrysohnSpace X] {s : Set X} {a : X} :
    a âˆˆ closure s â†” âˆƒ x : â„• â†’ X, (âˆ€ n : â„•, x n âˆˆ s) âˆ§ Tendsto x atTop (ğ“ a) := by
  rw [â† seqClosure_eq_closure]
  rfl

/-- If the domain of a function `f : Î± â†’ Î²` is a FrÃ©chet-Urysohn space, then convergence
is equivalent to sequential convergence. See also `Filter.tendsto_iff_seq_tendsto` for a version
that works for any pair of filters assuming that the filter in the domain is countably generated.

This property is equivalent to the definition of `FrechetUrysohnSpace`, see
`FrechetUrysohnSpace.of_seq_tendsto_imp_tendsto`. -/
theorem tendsto_nhds_iff_seq_tendsto [FrechetUrysohnSpace X] {f : X â†’ Y} {a : X} {b : Y} :
    Tendsto f (ğ“ a) (ğ“ b) â†” âˆ€ u : â„• â†’ X, Tendsto u atTop (ğ“ a) â†’ Tendsto (f âˆ˜ u) atTop (ğ“ b) := by
  refine
    âŸ¨fun hf u hu => hf.comp hu, fun h =>
      ((nhds_basis_closeds _).tendsto_iff (nhds_basis_closeds _)).2 ?_âŸ©
  rintro s âŸ¨hbs, hscâŸ©
  refine âŸ¨closure (f â»Â¹' s), âŸ¨mt ?_ hbs, isClosed_closureâŸ©, fun x => mt fun hx => subset_closure hxâŸ©
  rw [â† seqClosure_eq_closure]
  rintro âŸ¨u, hus, huâŸ©
  exact hsc.mem_of_tendsto (h u hu) (Eventually.of_forall hus)

/-- An alternative construction for `FrechetUrysohnSpace`: if sequential convergence implies
convergence, then the space is a FrÃ©chet-Urysohn space. -/
theorem FrechetUrysohnSpace.of_seq_tendsto_imp_tendsto
    (h : âˆ€ (f : X â†’ Prop) (a : X),
      (âˆ€ u : â„• â†’ X, Tendsto u atTop (ğ“ a) â†’ Tendsto (f âˆ˜ u) atTop (ğ“ (f a))) â†’ ContinuousAt f a) :
    FrechetUrysohnSpace X := by
  refine âŸ¨fun s x hcx => ?_âŸ©
  by_cases hx : x âˆˆ s
  Â· exact subset_seqClosure hx
  Â· obtain âŸ¨u, hux, husâŸ© : âˆƒ u : â„• â†’ X, Tendsto u atTop (ğ“ x) âˆ§ âˆƒá¶  x in atTop, u x âˆˆ s := by
      simpa only [ContinuousAt, hx, tendsto_nhds_true, (Â· âˆ˜ Â·), â† not_frequently, exists_prop,
        â† mem_closure_iff_frequently, hcx, imp_false, not_forall, not_not, not_false_eq_true,
        not_true_eq_false] using h (Â· âˆ‰ s) x
    rcases extraction_of_frequently_atTop hus with âŸ¨Ï†, Ï†_mono, hÏ†âŸ©
    exact âŸ¨u âˆ˜ Ï†, hÏ†, hux.comp Ï†_mono.tendsto_atTopâŸ©

-- see Note [lower instance priority]
/-- Every first-countable space is a FrÃ©chet-Urysohn space. -/
instance (priority := 100) FirstCountableTopology.frechetUrysohnSpace
    [FirstCountableTopology X] : FrechetUrysohnSpace X :=
  FrechetUrysohnSpace.of_seq_tendsto_imp_tendsto fun _ _ => tendsto_iff_seq_tendsto.2

-- see Note [lower instance priority]
/-- Every FrÃ©chet-Urysohn space is a sequential space. -/
instance (priority := 100) FrechetUrysohnSpace.to_sequentialSpace [FrechetUrysohnSpace X] :
    SequentialSpace X :=
  âŸ¨fun s hs => by rw [â† closure_eq_iff_isClosed, â† seqClosure_eq_closure, hs.seqClosure_eq]âŸ©

theorem Topology.IsInducing.frechetUrysohnSpace [FrechetUrysohnSpace Y] {f : X â†’ Y}
    (hf : IsInducing f) : FrechetUrysohnSpace X := by
  refine âŸ¨fun s x hx â†¦ ?_âŸ©
  rw [hf.closure_eq_preimage_closure_image, mem_preimage, mem_closure_iff_seq_limit] at hx
  rcases hx with âŸ¨u, hus, huâŸ©
  choose v hv hvu using hus
  refine âŸ¨v, hv, ?_âŸ©
  simpa only [hf.tendsto_nhds_iff, Function.comp_def, hvu]

/-- Subtype of a FrÃ©chet-Urysohn space is a FrÃ©chet-Urysohn space. -/
instance Subtype.instFrechetUrysohnSpace [FrechetUrysohnSpace X] {p : X â†’ Prop} :
    FrechetUrysohnSpace (Subtype p) :=
  IsInducing.subtypeVal.frechetUrysohnSpace

/-- In a sequential space, a set is closed iff it's sequentially closed. -/
theorem isSeqClosed_iff_isClosed [SequentialSpace X] {M : Set X} : IsSeqClosed M â†” IsClosed M :=
  âŸ¨IsSeqClosed.isClosed, IsClosed.isSeqClosedâŸ©

/-- The preimage of a sequentially closed set under a sequentially continuous map is sequentially
closed. -/
theorem IsSeqClosed.preimage {f : X â†’ Y} {s : Set Y} (hs : IsSeqClosed s) (hf : SeqContinuous f) :
    IsSeqClosed (f â»Â¹' s) := fun _x _p hx hp => hs hx (hf hp)

-- A continuous function is sequentially continuous.
protected theorem Continuous.seqContinuous {f : X â†’ Y} (hf : Continuous f) : SeqContinuous f :=
  fun _x p hx => (hf.tendsto p).comp hx

/-- A sequentially continuous function defined on a sequential space is continuous. -/
protected theorem SeqContinuous.continuous [SequentialSpace X] {f : X â†’ Y} (hf : SeqContinuous f) :
    Continuous f :=
  continuous_iff_isClosed.mpr fun _s hs => (hs.isSeqClosed.preimage hf).isClosed

/-- If the domain of a function is a sequential space, then continuity of this function is
equivalent to its sequential continuity. -/
theorem continuous_iff_seqContinuous [SequentialSpace X] {f : X â†’ Y} :
    Continuous f â†” SeqContinuous f :=
  âŸ¨Continuous.seqContinuous, SeqContinuous.continuousâŸ©

theorem SequentialSpace.coinduced [SequentialSpace X] {Y} (f : X â†’ Y) :
    @SequentialSpace Y (.coinduced f â€¹_â€º) :=
  letI : TopologicalSpace Y := .coinduced f â€¹_â€º
  âŸ¨fun _ hs â†¦ isClosed_coinduced.2 (hs.preimage continuous_coinduced_rng.seqContinuous).isClosedâŸ©

protected theorem SequentialSpace.iSup {X} {Î¹ : Sort*} {t : Î¹ â†’ TopologicalSpace X}
    (h : âˆ€ i, @SequentialSpace X (t i)) : @SequentialSpace X (â¨† i, t i) := by
  letI : TopologicalSpace X := â¨† i, t i
  refine âŸ¨fun s hs â†¦ isClosed_iSup_iff.2 fun i â†¦ ?_âŸ©
  letI := t i
  exact IsSeqClosed.isClosed fun u x hus hux â†¦ hs hus <| hux.mono_right <| nhds_mono <| le_iSup _ _

protected theorem SequentialSpace.sup {X} {tâ‚ tâ‚‚ : TopologicalSpace X}
    (hâ‚ : @SequentialSpace X tâ‚) (hâ‚‚ : @SequentialSpace X tâ‚‚) :
    @SequentialSpace X (tâ‚ âŠ” tâ‚‚) := by
  rw [sup_eq_iSup]
  exact .iSup <| Bool.forall_bool.2 âŸ¨hâ‚‚, hâ‚âŸ©

lemma Topology.IsQuotientMap.sequentialSpace [SequentialSpace X] {f : X â†’ Y}
    (hf : IsQuotientMap f) : SequentialSpace Y := hf.2.symm â–¸ .coinduced f

/-- The quotient of a sequential space is a sequential space. -/
instance Quotient.instSequentialSpace [SequentialSpace X] {s : Setoid X} :
    SequentialSpace (Quotient s) :=
  isQuotientMap_quot_mk.sequentialSpace

/-- The sum (disjoint union) of two sequential spaces is a sequential space. -/
instance Sum.instSequentialSpace [SequentialSpace X] [SequentialSpace Y] :
    SequentialSpace (X âŠ• Y) :=
  .sup (.coinduced Sum.inl) (.coinduced Sum.inr)

/-- The disjoint union of an indexed family of sequential spaces is a sequential space. -/
instance Sigma.instSequentialSpace {Î¹ : Type*} {X : Î¹ â†’ Type*}
    [âˆ€ i, TopologicalSpace (X i)] [âˆ€ i, SequentialSpace (X i)] : SequentialSpace (Î£ i, X i) :=
  .iSup fun _ â†¦ .coinduced _

end TopologicalSpace

section SeqCompact

open TopologicalSpace FirstCountableTopology

variable [TopologicalSpace X]

theorem IsSeqCompact.subseq_of_frequently_in {s : Set X} (hs : IsSeqCompact s) {x : â„• â†’ X}
    (hx : âˆƒá¶  n in atTop, x n âˆˆ s) :
    âˆƒ a âˆˆ s, âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ Tendsto (x âˆ˜ Ï†) atTop (ğ“ a) :=
  let âŸ¨Ïˆ, hÏˆ, huÏˆâŸ© := extraction_of_frequently_atTop hx
  let âŸ¨a, a_in, Ï†, hÏ†, hâŸ© := hs huÏˆ
  âŸ¨a, a_in, Ïˆ âˆ˜ Ï†, hÏˆ.comp hÏ†, hâŸ©

theorem SeqCompactSpace.tendsto_subseq [SeqCompactSpace X] (x : â„• â†’ X) :
    âˆƒ (a : X) (Ï† : â„• â†’ â„•), StrictMono Ï† âˆ§ Tendsto (x âˆ˜ Ï†) atTop (ğ“ a) :=
  let âŸ¨a, _, Ï†, mono, hâŸ© := isSeqCompact_univ fun n => mem_univ (x n)
  âŸ¨a, Ï†, mono, hâŸ©

section FirstCountableTopology

variable [FirstCountableTopology X]

open FirstCountableTopology

protected theorem IsCompact.isSeqCompact {s : Set X} (hs : IsCompact s) : IsSeqCompact s :=
  fun _x x_in =>
  let âŸ¨a, a_in, haâŸ© := hs (tendsto_principal.mpr (Eventually.of_forall x_in))
  âŸ¨a, a_in, tendsto_subseq haâŸ©

theorem IsCompact.tendsto_subseq' {s : Set X} {x : â„• â†’ X} (hs : IsCompact s)
    (hx : âˆƒá¶  n in atTop, x n âˆˆ s) :
    âˆƒ a âˆˆ s, âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ Tendsto (x âˆ˜ Ï†) atTop (ğ“ a) :=
  hs.isSeqCompact.subseq_of_frequently_in hx

theorem IsCompact.tendsto_subseq {s : Set X} {x : â„• â†’ X} (hs : IsCompact s) (hx : âˆ€ n, x n âˆˆ s) :
    âˆƒ a âˆˆ s, âˆƒ Ï† : â„• â†’ â„•, StrictMono Ï† âˆ§ Tendsto (x âˆ˜ Ï†) atTop (ğ“ a) :=
  hs.isSeqCompact hx

-- see Note [lower instance priority]
instance (priority := 100) FirstCountableTopology.seq_compact_of_compact [CompactSpace X] :
    SeqCompactSpace X :=
  âŸ¨isCompact_univ.isSeqCompactâŸ©

theorem CompactSpace.tendsto_subseq [CompactSpace X] (x : â„• â†’ X) :
    âˆƒ (a : _) (Ï† : â„• â†’ â„•), StrictMono Ï† âˆ§ Tendsto (x âˆ˜ Ï†) atTop (ğ“ a) :=
  SeqCompactSpace.tendsto_subseq x

end FirstCountableTopology

section Image

variable [TopologicalSpace Y] {f : X â†’ Y}

/-- Sequential compactness of sets is preserved under sequentially continuous functions. -/
theorem IsSeqCompact.image (f_cont : SeqContinuous f) {K : Set X} (K_cpt : IsSeqCompact K) :
    IsSeqCompact (f '' K) := by
  intro ys ys_in_fK
  choose xs xs_in_K fxs_eq_ys using ys_in_fK
  obtain âŸ¨a, a_in_K, phi, phi_mono, xs_phi_limâŸ© := K_cpt xs_in_K
  refine âŸ¨f a, mem_image_of_mem f a_in_K, phi, phi_mono, ?_âŸ©
  exact (f_cont xs_phi_lim).congr fun x â†¦ fxs_eq_ys (phi x)

/-- The range of sequentially continuous function on a sequentially compact space is sequentially
compact. -/
theorem IsSeqCompact.range [SeqCompactSpace X] (f_cont : SeqContinuous f) :
    IsSeqCompact (Set.range f) := by
  simpa using isSeqCompact_univ.image f_cont

end Image

end SeqCompact

section UniformSpaceSeqCompact

open uniformity

open UniformSpace Prod

variable [UniformSpace X] {s : Set X}

theorem IsSeqCompact.exists_tendsto_of_frequently_mem (hs : IsSeqCompact s) {u : â„• â†’ X}
    (hu : âˆƒá¶  n in atTop, u n âˆˆ s) (huc : CauchySeq u) : âˆƒ x âˆˆ s, Tendsto u atTop (ğ“ x) :=
  let âŸ¨x, hxs, _Ï†, Ï†_mono, hxâŸ© := hs.subseq_of_frequently_in hu
  âŸ¨x, hxs, tendsto_nhds_of_cauchySeq_of_subseq huc Ï†_mono.tendsto_atTop hxâŸ©

theorem IsSeqCompact.exists_tendsto (hs : IsSeqCompact s) {u : â„• â†’ X} (hu : âˆ€ n, u n âˆˆ s)
    (huc : CauchySeq u) : âˆƒ x âˆˆ s, Tendsto u atTop (ğ“ x) :=
  hs.exists_tendsto_of_frequently_mem (Frequently.of_forall hu) huc

/-- A sequentially compact set in a uniform space is totally bounded. -/
protected theorem IsSeqCompact.totallyBounded (h : IsSeqCompact s) : TotallyBounded s := by
  intro V V_in
  unfold IsSeqCompact at h
  contrapose! h
  obtain âŸ¨u, u_in, huâŸ© : âˆƒ u : â„• â†’ X, (âˆ€ n, u n âˆˆ s) âˆ§ âˆ€ n m, m < n â†’ u m âˆ‰ ball (u n) V := by
    simp only [not_subset, mem_iUnionâ‚‚, not_exists, exists_prop] at h
    simpa only [forall_and, forall_mem_image, not_and] using seq_of_forall_finite_exists h
  refine âŸ¨u, u_in, fun x _ Ï† hÏ† huÏ† => ?_âŸ©
  obtain âŸ¨N, hNâŸ© : âˆƒ N, âˆ€ p q, p â‰¥ N â†’ q â‰¥ N â†’ (u (Ï† p), u (Ï† q)) âˆˆ V :=
    huÏ†.cauchySeq.mem_entourage V_in
  exact hu (Ï† <| N + 1) (Ï† N) (hÏ† <| Nat.lt_add_one N) (hN (N + 1) N N.le_succ le_rfl)

variable [IsCountablyGenerated (ğ“¤ X)]

/-- A sequentially compact set in a uniform space with countably generated uniformity filter
is complete. -/
protected theorem IsSeqCompact.isComplete (hs : IsSeqCompact s) : IsComplete s := fun l hl hls => by
  have := hl.1
  rcases exists_antitone_basis (ğ“¤ X) with âŸ¨V, hVâŸ©
  choose W hW hWV using fun n => comp_mem_uniformity_sets (hV.mem n)
  have hWV' : âˆ€ n, W n âŠ† V n := fun n âŸ¨x, yâŸ© hx =>
    @hWV n (x, y) âŸ¨x, refl_mem_uniformity <| hW _, hxâŸ©
  obtain âŸ¨t, ht_anti, htl, htW, htsâŸ© :
      âˆƒ t : â„• â†’ Set X, Antitone t âˆ§ (âˆ€ n, t n âˆˆ l) âˆ§ (âˆ€ n, t n Ã—Ë¢ t n âŠ† W n) âˆ§ âˆ€ n, t n âŠ† s := by
    have : âˆ€ n, âˆƒ t âˆˆ l, t Ã—Ë¢ t âŠ† W n âˆ§ t âŠ† s := by
      rw [le_principal_iff] at hls
      have : âˆ€ n, W n âˆ© s Ã—Ë¢ s âˆˆ l Ã—Ë¢ l := fun n => inter_mem (hl.2 (hW n)) (prod_mem_prod hls hls)
      simpa only [l.basis_sets.prod_self.mem_iff, true_imp_iff, subset_inter_iff,
        prod_self_subset_prod_self, and_assoc] using this
    choose t htl htW hts using this
    have : âˆ€ n : â„•, â‹‚ k â‰¤ n, t k âŠ† t n := fun n => by apply iInterâ‚‚_subset; rfl
    exact âŸ¨fun n => â‹‚ k â‰¤ n, t k, fun m n h =>
      biInter_subset_biInter_left fun k (hk : k â‰¤ m) => hk.trans h, fun n =>
      (biInter_mem (finite_le_nat n)).2 fun k _ => htl k, fun n =>
      (prod_mono (this n) (this n)).trans (htW n), fun n => (this n).trans (hts n)âŸ©
  choose u hu using fun n => Filter.nonempty_of_mem (htl n)
  have huc : CauchySeq u := hV.toHasBasis.cauchySeq_iff.2 fun N _ =>
      âŸ¨N, fun m hm n hn => hWV' _ <| @htW N (_, _) âŸ¨ht_anti hm (hu _), ht_anti hn (hu _)âŸ©âŸ©
  rcases hs.exists_tendsto (fun n => hts n (hu n)) huc with âŸ¨x, hxs, hxâŸ©
  refine âŸ¨x, hxs, (nhds_basis_uniformity' hV.toHasBasis).ge_iff.2 fun N _ => ?_âŸ©
  obtain âŸ¨n, hNn, hnâŸ© : âˆƒ n, N â‰¤ n âˆ§ u n âˆˆ ball x (W N) :=
    ((eventually_ge_atTop N).and (hx <| ball_mem_nhds x (hW N))).exists
  refine mem_of_superset (htl n) fun y hy => hWV N âŸ¨u n, hn, htW N ?_âŸ©
  exact âŸ¨ht_anti hNn (hu n), ht_anti hNn hyâŸ©

/-- If `ğ“¤ Î²` is countably generated, then any sequentially compact set is compact. -/
protected theorem IsSeqCompact.isCompact (hs : IsSeqCompact s) : IsCompact s :=
  isCompact_iff_totallyBounded_isComplete.2 âŸ¨hs.totallyBounded, hs.isCompleteâŸ©

/-- A version of Bolzano-Weierstrass: in a uniform space with countably generated uniformity filter
(e.g., in a metric space), a set is compact if and only if it is sequentially compact. -/
protected theorem UniformSpace.isCompact_iff_isSeqCompact : IsCompact s â†” IsSeqCompact s :=
  âŸ¨fun H => H.isSeqCompact, fun H => H.isCompactâŸ©

theorem UniformSpace.compactSpace_iff_seqCompactSpace : CompactSpace X â†” SeqCompactSpace X := by
  simp only [â† isCompact_univ_iff, seqCompactSpace_iff, UniformSpace.isCompact_iff_isSeqCompact]

end UniformSpaceSeqCompact
