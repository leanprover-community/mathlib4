/-
Copyright (c) 2018 Jan-David Salchow. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, Patrick Massot, Yury Kudryashov
-/
import Mathlib.Topology.Defs.Sequences
import Mathlib.Topology.UniformSpace.Cauchy

/-!
# Sequences in topological spaces

In this file we prove theorems about relations
between closure/compactness/continuity etc and their sequential counterparts.

## Main definitions

The following notions are defined in `Topology/Defs/Sequences`.
We build theory about these definitions here, so we remind the definitions.

### Set operation
* `seqClosure s`: sequential closure of a set, the set of limits of sequences of points of `s`;

### Predicates

* `IsSeqClosed s`: predicate saying that a set is sequentially closed, i.e., `seqClosure s ‚äÜ s`;
* `SeqContinuous f`: predicate saying that a function is sequentially continuous, i.e.,
  for any sequence `u : ‚Ñï ‚Üí X` that converges to a point `x`, the sequence `f ‚àò u` converges to
  `f x`;
* `IsSeqCompact s`: predicate saying that a set is sequentially compact, i.e., every sequence
  taking values in `s` has a converging subsequence.

### Type classes

* `FrechetUrysohnSpace X`: a typeclass saying that a topological space is a *Fr√©chet-Urysohn
  space*, i.e., the sequential closure of any set is equal to its closure.
* `SequentialSpace X`: a typeclass saying that a topological space is a *sequential space*, i.e.,
  any sequentially closed set in this space is closed. This condition is weaker than being a
  Fr√©chet-Urysohn space.
* `SeqCompactSpace X`: a typeclass saying that a topological space is sequentially compact, i.e.,
  every sequence in `X` has a converging subsequence.

## Main results

* `seqClosure_subset_closure`: closure of a set includes its sequential closure;
* `IsClosed.isSeqClosed`: a closed set is sequentially closed;
* `IsSeqClosed.seqClosure_eq`: sequential closure of a sequentially closed set `s` is equal
  to `s`;
* `seqClosure_eq_closure`: in a Fr√©chet-Urysohn space, the sequential closure of a set is equal to
  its closure;
* `tendsto_nhds_iff_seq_tendsto`, `FrechetUrysohnSpace.of_seq_tendsto_imp_tendsto`: a topological
  space is a Fr√©chet-Urysohn space if and only if sequential convergence implies convergence;
* `FirstCountableTopology.frechetUrysohnSpace`: every topological space with
  first countable topology is a Fr√©chet-Urysohn space;
* `FrechetUrysohnSpace.to_sequentialSpace`: every Fr√©chet-Urysohn space is a sequential space;
* `IsSeqCompact.isCompact`: a sequentially compact set in a uniform space with countably
  generated uniformity is compact.

## Tags

sequentially closed, sequentially compact, sequential space
-/


open Set Function Filter TopologicalSpace Bornology
open scoped Topology Uniformity

variable {X Y : Type*}

/-! ### Sequential closures, sequential continuity, and sequential spaces. -/

section TopologicalSpace

variable [TopologicalSpace X] [TopologicalSpace Y]

theorem subset_seqClosure {s : Set X} : s ‚äÜ seqClosure s := fun p hp =>
  ‚ü®const ‚Ñï p, fun _ => hp, tendsto_const_nhds‚ü©

/-- The sequential closure of a set is contained in the closure of that set.
The converse is not true. -/
theorem seqClosure_subset_closure {s : Set X} : seqClosure s ‚äÜ closure s := fun _p ‚ü®_x, xM, xp‚ü© =>
  mem_closure_of_tendsto xp (univ_mem' xM)

/-- The sequential closure of a sequentially closed set is the set itself. -/
theorem IsSeqClosed.seqClosure_eq {s : Set X} (hs : IsSeqClosed s) : seqClosure s = s :=
  Subset.antisymm (fun _p ‚ü®_x, hx, hp‚ü© => hs hx hp) subset_seqClosure

/-- If a set is equal to its sequential closure, then it is sequentially closed. -/
theorem isSeqClosed_of_seqClosure_eq {s : Set X} (hs : seqClosure s = s) : IsSeqClosed s :=
  fun x _p hxs hxp => hs ‚ñ∏ ‚ü®x, hxs, hxp‚ü©

/-- A set is sequentially closed iff it is equal to its sequential closure. -/
theorem isSeqClosed_iff {s : Set X} : IsSeqClosed s ‚Üî seqClosure s = s :=
  ‚ü®IsSeqClosed.seqClosure_eq, isSeqClosed_of_seqClosure_eq‚ü©

/-- A set is sequentially closed if it is closed. -/
protected theorem IsClosed.isSeqClosed {s : Set X} (hc : IsClosed s) : IsSeqClosed s :=
  fun _u _x hu hx => hc.mem_of_tendsto hx (eventually_of_forall hu)

theorem seqClosure_eq_closure [FrechetUrysohnSpace X] (s : Set X) : seqClosure s = closure s :=
  seqClosure_subset_closure.antisymm <| FrechetUrysohnSpace.closure_subset_seqClosure s

/-- In a Fr√©chet-Urysohn space, a point belongs to the closure of a set iff it is a limit
of a sequence taking values in this set. -/
theorem mem_closure_iff_seq_limit [FrechetUrysohnSpace X] {s : Set X} {a : X} :
    a ‚àà closure s ‚Üî ‚àÉ x : ‚Ñï ‚Üí X, (‚àÄ n : ‚Ñï, x n ‚àà s) ‚àß Tendsto x atTop (ùìù a) := by
  rw [‚Üê seqClosure_eq_closure]
  rfl

/-- If the domain of a function `f : Œ± ‚Üí Œ≤` is a Fr√©chet-Urysohn space, then convergence
is equivalent to sequential convergence. See also `Filter.tendsto_iff_seq_tendsto` for a version
that works for any pair of filters assuming that the filter in the domain is countably generated.

This property is equivalent to the definition of `FrechetUrysohnSpace`, see
`FrechetUrysohnSpace.of_seq_tendsto_imp_tendsto`. -/
theorem tendsto_nhds_iff_seq_tendsto [FrechetUrysohnSpace X] {f : X ‚Üí Y} {a : X} {b : Y} :
    Tendsto f (ùìù a) (ùìù b) ‚Üî ‚àÄ u : ‚Ñï ‚Üí X, Tendsto u atTop (ùìù a) ‚Üí Tendsto (f ‚àò u) atTop (ùìù b) := by
  refine
    ‚ü®fun hf u hu => hf.comp hu, fun h =>
      ((nhds_basis_closeds _).tendsto_iff (nhds_basis_closeds _)).2 ?_‚ü©
  rintro s ‚ü®hbs, hsc‚ü©
  refine ‚ü®closure (f ‚Åª¬π' s), ‚ü®mt ?_ hbs, isClosed_closure‚ü©, fun x => mt fun hx => subset_closure hx‚ü©
  rw [‚Üê seqClosure_eq_closure]
  rintro ‚ü®u, hus, hu‚ü©
  exact hsc.mem_of_tendsto (h u hu) (eventually_of_forall hus)

/-- An alternative construction for `FrechetUrysohnSpace`: if sequential convergence implies
convergence, then the space is a Fr√©chet-Urysohn space. -/
theorem FrechetUrysohnSpace.of_seq_tendsto_imp_tendsto
    (h : ‚àÄ (f : X ‚Üí Prop) (a : X),
      (‚àÄ u : ‚Ñï ‚Üí X, Tendsto u atTop (ùìù a) ‚Üí Tendsto (f ‚àò u) atTop (ùìù (f a))) ‚Üí ContinuousAt f a) :
    FrechetUrysohnSpace X := by
  refine ‚ü®fun s x hcx => ?_‚ü©
  by_cases hx : x ‚àà s
  ¬∑ exact subset_seqClosure hx
  ¬∑ obtain ‚ü®u, hux, hus‚ü© : ‚àÉ u : ‚Ñï ‚Üí X, Tendsto u atTop (ùìù x) ‚àß ‚àÉ·∂† x in atTop, u x ‚àà s := by
      simpa only [ContinuousAt, hx, tendsto_nhds_true, (¬∑ ‚àò ¬∑), ‚Üê not_frequently, exists_prop,
        ‚Üê mem_closure_iff_frequently, hcx, imp_false, not_forall, not_not, not_false_eq_true,
        not_true_eq_false] using h (¬∑ ‚àâ s) x
    rcases extraction_of_frequently_atTop hus with ‚ü®œÜ, œÜ_mono, hœÜ‚ü©
    exact ‚ü®u ‚àò œÜ, hœÜ, hux.comp œÜ_mono.tendsto_atTop‚ü©

-- see Note [lower instance priority]
/-- Every first-countable space is a Fr√©chet-Urysohn space. -/
instance (priority := 100) FirstCountableTopology.frechetUrysohnSpace
    [FirstCountableTopology X] : FrechetUrysohnSpace X :=
  FrechetUrysohnSpace.of_seq_tendsto_imp_tendsto fun _ _ => tendsto_iff_seq_tendsto.2

-- see Note [lower instance priority]
/-- Every Fr√©chet-Urysohn space is a sequential space. -/
instance (priority := 100) FrechetUrysohnSpace.to_sequentialSpace [FrechetUrysohnSpace X] :
    SequentialSpace X :=
  ‚ü®fun s hs => by rw [‚Üê closure_eq_iff_isClosed, ‚Üê seqClosure_eq_closure, hs.seqClosure_eq]‚ü©

theorem Inducing.frechetUrysohnSpace [FrechetUrysohnSpace Y] {f : X ‚Üí Y} (hf : Inducing f) :
    FrechetUrysohnSpace X := by
  refine ‚ü®fun s x hx ‚Ü¶ ?_‚ü©
  rw [hf.closure_eq_preimage_closure_image, mem_preimage, mem_closure_iff_seq_limit] at hx
  rcases hx with ‚ü®u, hus, hu‚ü©
  choose v hv hvu using hus
  refine ‚ü®v, hv, ?_‚ü©
  simpa only [hf.tendsto_nhds_iff, (¬∑ ‚àò ¬∑), hvu]

/-- Subtype of a Fr√©chet-Urysohn space is a Fr√©chet-Urysohn space. -/
instance Subtype.instFrechetUrysohnSpace [FrechetUrysohnSpace X] {p : X ‚Üí Prop} :
    FrechetUrysohnSpace (Subtype p) :=
  inducing_subtype_val.frechetUrysohnSpace

/-- In a sequential space, a set is closed iff it's sequentially closed. -/
theorem isSeqClosed_iff_isClosed [SequentialSpace X] {M : Set X} : IsSeqClosed M ‚Üî IsClosed M :=
  ‚ü®IsSeqClosed.isClosed, IsClosed.isSeqClosed‚ü©

/-- The preimage of a sequentially closed set under a sequentially continuous map is sequentially
closed. -/
theorem IsSeqClosed.preimage {f : X ‚Üí Y} {s : Set Y} (hs : IsSeqClosed s) (hf : SeqContinuous f) :
    IsSeqClosed (f ‚Åª¬π' s) := fun _x _p hx hp => hs hx (hf hp)

-- A continuous function is sequentially continuous.
protected theorem Continuous.seqContinuous {f : X ‚Üí Y} (hf : Continuous f) : SeqContinuous f :=
  fun _x p hx => (hf.tendsto p).comp hx

/-- A sequentially continuous function defined on a sequential space is continuous. -/
protected theorem SeqContinuous.continuous [SequentialSpace X] {f : X ‚Üí Y} (hf : SeqContinuous f) :
    Continuous f :=
  continuous_iff_isClosed.mpr fun _s hs => (hs.isSeqClosed.preimage hf).isClosed

/-- If the domain of a function is a sequential space, then continuity of this function is
equivalent to its sequential continuity. -/
theorem continuous_iff_seqContinuous [SequentialSpace X] {f : X ‚Üí Y} :
    Continuous f ‚Üî SeqContinuous f :=
  ‚ü®Continuous.seqContinuous, SeqContinuous.continuous‚ü©

theorem SequentialSpace.coinduced [SequentialSpace X] (f : X ‚Üí Y) :
    @SequentialSpace Y (.coinduced f ‚Äπ_‚Ä∫) :=
  letI : TopologicalSpace Y := .coinduced f ‚Äπ_‚Ä∫
  ‚ü®fun s hs ‚Ü¶ isClosed_coinduced.2 (hs.preimage continuous_coinduced_rng.seqContinuous).isClosed‚ü©

protected theorem SequentialSpace.iSup {Œπ : Sort*} {t : Œπ ‚Üí TopologicalSpace X}
    (h : ‚àÄ i, @SequentialSpace X (t i)) : @SequentialSpace X (‚®Ü i, t i) := by
  letI : TopologicalSpace X := ‚®Ü i, t i
  refine ‚ü®fun s hs ‚Ü¶ isClosed_iSup_iff.2 fun i ‚Ü¶ ?_‚ü©
  letI := t i
  exact IsSeqClosed.isClosed fun u x hus hux ‚Ü¶ hs hus <| hux.mono_right <| nhds_mono <| le_iSup _ _

protected theorem SequentialSpace.sup {t‚ÇÅ t‚ÇÇ : TopologicalSpace X}
    (h‚ÇÅ : @SequentialSpace X t‚ÇÅ) (h‚ÇÇ : @SequentialSpace X t‚ÇÇ) :
    @SequentialSpace X (t‚ÇÅ ‚äî t‚ÇÇ) := by
  rw [sup_eq_iSup]
  exact .iSup <| Bool.forall_bool.2 ‚ü®h‚ÇÇ, h‚ÇÅ‚ü©

theorem QuotientMap.sequentialSpace [SequentialSpace X] {f : X ‚Üí Y} (hf : QuotientMap f) :
    SequentialSpace Y :=
  hf.2.symm ‚ñ∏ .coinduced f

/-- The quotient of a sequential space is a sequential space. -/
instance Quotient.instSequentialSpace [SequentialSpace X] {s : Setoid X} :
    SequentialSpace (Quotient s) :=
  quotientMap_quot_mk.sequentialSpace

/-- The sum (disjoint union) of two sequential spaces is a sequential space. -/
instance Sum.instSequentialSpace [SequentialSpace X] [SequentialSpace Y] :
    SequentialSpace (X ‚äï Y) :=
  .sup (.coinduced Sum.inl) (.coinduced Sum.inr)

/-- The disjoint union of an indexed family of sequential spaces is a sequential space. -/
instance Sigma.instSequentialSpace {Œπ : Type*} {X : Œπ ‚Üí Type*}
    [‚àÄ i, TopologicalSpace (X i)] [‚àÄ i, SequentialSpace (X i)] : SequentialSpace (Œ£ i, X i) :=
  .iSup fun _ ‚Ü¶ .coinduced _

end TopologicalSpace

section SeqCompact

open TopologicalSpace FirstCountableTopology

variable [TopologicalSpace X]

theorem IsSeqCompact.subseq_of_frequently_in {s : Set X} (hs : IsSeqCompact s) {x : ‚Ñï ‚Üí X}
    (hx : ‚àÉ·∂† n in atTop, x n ‚àà s) :
    ‚àÉ a ‚àà s, ‚àÉ œÜ : ‚Ñï ‚Üí ‚Ñï, StrictMono œÜ ‚àß Tendsto (x ‚àò œÜ) atTop (ùìù a) :=
  let ‚ü®œà, hœà, huœà‚ü© := extraction_of_frequently_atTop hx
  let ‚ü®a, a_in, œÜ, hœÜ, h‚ü© := hs huœà
  ‚ü®a, a_in, œà ‚àò œÜ, hœà.comp hœÜ, h‚ü©

theorem SeqCompactSpace.tendsto_subseq [SeqCompactSpace X] (x : ‚Ñï ‚Üí X) :
    ‚àÉ (a : X) (œÜ : ‚Ñï ‚Üí ‚Ñï), StrictMono œÜ ‚àß Tendsto (x ‚àò œÜ) atTop (ùìù a) :=
  let ‚ü®a, _, œÜ, mono, h‚ü© := seq_compact_univ fun n => mem_univ (x n)
  ‚ü®a, œÜ, mono, h‚ü©

section FirstCountableTopology

variable [FirstCountableTopology X]

open FirstCountableTopology

protected theorem IsCompact.isSeqCompact {s : Set X} (hs : IsCompact s) : IsSeqCompact s :=
  fun _x x_in =>
  let ‚ü®a, a_in, ha‚ü© := hs (tendsto_principal.mpr (eventually_of_forall x_in))
  ‚ü®a, a_in, tendsto_subseq ha‚ü©

theorem IsCompact.tendsto_subseq' {s : Set X} {x : ‚Ñï ‚Üí X} (hs : IsCompact s)
    (hx : ‚àÉ·∂† n in atTop, x n ‚àà s) :
    ‚àÉ a ‚àà s, ‚àÉ œÜ : ‚Ñï ‚Üí ‚Ñï, StrictMono œÜ ‚àß Tendsto (x ‚àò œÜ) atTop (ùìù a) :=
  hs.isSeqCompact.subseq_of_frequently_in hx

theorem IsCompact.tendsto_subseq {s : Set X} {x : ‚Ñï ‚Üí X} (hs : IsCompact s) (hx : ‚àÄ n, x n ‚àà s) :
    ‚àÉ a ‚àà s, ‚àÉ œÜ : ‚Ñï ‚Üí ‚Ñï, StrictMono œÜ ‚àß Tendsto (x ‚àò œÜ) atTop (ùìù a) :=
  hs.isSeqCompact hx

-- see Note [lower instance priority]
instance (priority := 100) FirstCountableTopology.seq_compact_of_compact [CompactSpace X] :
    SeqCompactSpace X :=
  ‚ü®isCompact_univ.isSeqCompact‚ü©

theorem CompactSpace.tendsto_subseq [CompactSpace X] (x : ‚Ñï ‚Üí X) :
    ‚àÉ (a : _) (œÜ : ‚Ñï ‚Üí ‚Ñï), StrictMono œÜ ‚àß Tendsto (x ‚àò œÜ) atTop (ùìù a) :=
  SeqCompactSpace.tendsto_subseq x

end FirstCountableTopology

section Image

/-- Sequential compactness of sets is preserved under sequentially continuous functions. -/
theorem IsSeqCompact.image {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] (f : X ‚Üí Y)
    (f_cont : SeqContinuous f) {K : Set X} (K_cpt : IsSeqCompact K) :
    IsSeqCompact (f '' K) := by
  intro ys ys_in_fK
  choose xs xs_in_K fxs_eq_ys using fun n ‚Ü¶ (ys_in_fK n)
  simp only [Set.mem_image, exists_exists_and_eq_and]
  obtain ‚ü®a, a_in_K, phi, phi_mono, xs_phi_lim‚ü© := K_cpt xs_in_K
  refine ‚ü®a, a_in_K, phi, phi_mono, ?_‚ü©
  exact Filter.Tendsto.congr (fun x ‚Ü¶ fxs_eq_ys (phi x)) (f_cont xs_phi_lim)

/-- The range of sequentially continuous function on a sequentially compact space is sequentially
compact. -/
theorem isSeqCompact_range {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    [SeqCompactSpace X] (f : X ‚Üí Y) (hf : SeqContinuous f) :
    IsSeqCompact (Set.range f) := by
  rw [‚Üê Set.image_univ]
  exact IsSeqCompact.image f hf ((seqCompactSpace_iff X).mp ‚ÄπSeqCompactSpace X‚Ä∫)

/-- Sequential compactness of sets is preserved under continuous functions. -/
theorem IsSeqCompact.image_of_continuous {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    (f : X ‚Üí Y) (f_cont : Continuous f) {K : Set X} (K_cpt : IsSeqCompact K) :
    IsSeqCompact (f '' K) :=
  IsSeqCompact.image f (Continuous.seqContinuous f_cont) K_cpt

/-- The range of continuous function on a sequentially compact space is sequentially compact. -/
theorem SeqCompactSpace.range_of_continuous {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    [SeqCompactSpace X] (f : X ‚Üí Y) (f_cont : Continuous f) :
    IsSeqCompact (Set.range f) :=
  isSeqCompact_range f (Continuous.seqContinuous f_cont)

end Image

end SeqCompact

section UniformSpaceSeqCompact

open uniformity

open UniformSpace Prod

variable [UniformSpace X] {s : Set X}

theorem IsSeqCompact.exists_tendsto_of_frequently_mem (hs : IsSeqCompact s) {u : ‚Ñï ‚Üí X}
    (hu : ‚àÉ·∂† n in atTop, u n ‚àà s) (huc : CauchySeq u) : ‚àÉ x ‚àà s, Tendsto u atTop (ùìù x) :=
  let ‚ü®x, hxs, _œÜ, œÜ_mono, hx‚ü© := hs.subseq_of_frequently_in hu
  ‚ü®x, hxs, tendsto_nhds_of_cauchySeq_of_subseq huc œÜ_mono.tendsto_atTop hx‚ü©

theorem IsSeqCompact.exists_tendsto (hs : IsSeqCompact s) {u : ‚Ñï ‚Üí X} (hu : ‚àÄ n, u n ‚àà s)
    (huc : CauchySeq u) : ‚àÉ x ‚àà s, Tendsto u atTop (ùìù x) :=
  hs.exists_tendsto_of_frequently_mem (frequently_of_forall hu) huc

/-- A sequentially compact set in a uniform space is totally bounded. -/
protected theorem IsSeqCompact.totallyBounded (h : IsSeqCompact s) : TotallyBounded s := by
  intro V V_in
  unfold IsSeqCompact at h
  contrapose! h
  obtain ‚ü®u, u_in, hu‚ü© : ‚àÉ u : ‚Ñï ‚Üí X, (‚àÄ n, u n ‚àà s) ‚àß ‚àÄ n m, m < n ‚Üí u m ‚àâ ball (u n) V := by
    simp only [not_subset, mem_iUnion‚ÇÇ, not_exists, exists_prop] at h
    simpa only [forall_and, forall_mem_image, not_and] using seq_of_forall_finite_exists h
  refine ‚ü®u, u_in, fun x _ œÜ hœÜ huœÜ => ?_‚ü©
  obtain ‚ü®N, hN‚ü© : ‚àÉ N, ‚àÄ p q, p ‚â• N ‚Üí q ‚â• N ‚Üí (u (œÜ p), u (œÜ q)) ‚àà V :=
    huœÜ.cauchySeq.mem_entourage V_in
  exact hu (œÜ <| N + 1) (œÜ N) (hœÜ <| lt_add_one N) (hN (N + 1) N N.le_succ le_rfl)

variable [IsCountablyGenerated (ùì§ X)]

/-- A sequentially compact set in a uniform set with countably generated uniformity filter
is complete. -/
protected theorem IsSeqCompact.isComplete (hs : IsSeqCompact s) : IsComplete s := fun l hl hls => by
  have := hl.1
  rcases exists_antitone_basis (ùì§ X) with ‚ü®V, hV‚ü©
  choose W hW hWV using fun n => comp_mem_uniformity_sets (hV.mem n)
  have hWV' : ‚àÄ n, W n ‚äÜ V n := fun n ‚ü®x, y‚ü© hx =>
    @hWV n (x, y) ‚ü®x, refl_mem_uniformity <| hW _, hx‚ü©
  obtain ‚ü®t, ht_anti, htl, htW, hts‚ü© :
      ‚àÉ t : ‚Ñï ‚Üí Set X, Antitone t ‚àß (‚àÄ n, t n ‚àà l) ‚àß (‚àÄ n, t n √óÀ¢ t n ‚äÜ W n) ‚àß ‚àÄ n, t n ‚äÜ s := by
    have : ‚àÄ n, ‚àÉ t ‚àà l, t √óÀ¢ t ‚äÜ W n ‚àß t ‚äÜ s := by
      rw [le_principal_iff] at hls
      have : ‚àÄ n, W n ‚à© s √óÀ¢ s ‚àà l √óÀ¢ l := fun n => inter_mem (hl.2 (hW n)) (prod_mem_prod hls hls)
      simpa only [l.basis_sets.prod_self.mem_iff, true_imp_iff, subset_inter_iff,
        prod_self_subset_prod_self, and_assoc] using this
    choose t htl htW hts using this
    have : ‚àÄ n : ‚Ñï, ‚ãÇ k ‚â§ n, t k ‚äÜ t n := fun n => by apply iInter‚ÇÇ_subset; rfl
    exact ‚ü®fun n => ‚ãÇ k ‚â§ n, t k, fun m n h =>
      biInter_subset_biInter_left fun k (hk : k ‚â§ m) => hk.trans h, fun n =>
      (biInter_mem (finite_le_nat n)).2 fun k _ => htl k, fun n =>
      (prod_mono (this n) (this n)).trans (htW n), fun n => (this n).trans (hts n)‚ü©
  choose u hu using fun n => Filter.nonempty_of_mem (htl n)
  have huc : CauchySeq u := hV.toHasBasis.cauchySeq_iff.2 fun N _ =>
      ‚ü®N, fun m hm n hn => hWV' _ <| @htW N (_, _) ‚ü®ht_anti hm (hu _), ht_anti hn (hu _)‚ü©‚ü©
  rcases hs.exists_tendsto (fun n => hts n (hu n)) huc with ‚ü®x, hxs, hx‚ü©
  refine ‚ü®x, hxs, (nhds_basis_uniformity' hV.toHasBasis).ge_iff.2 fun N _ => ?_‚ü©
  obtain ‚ü®n, hNn, hn‚ü© : ‚àÉ n, N ‚â§ n ‚àß u n ‚àà ball x (W N) :=
    ((eventually_ge_atTop N).and (hx <| ball_mem_nhds x (hW N))).exists
  refine mem_of_superset (htl n) fun y hy => hWV N ‚ü®u n, hn, htW N ?_‚ü©
  exact ‚ü®ht_anti hNn (hu n), ht_anti hNn hy‚ü©

/-- If `ùì§ Œ≤` is countably generated, then any sequentially compact set is compact. -/
protected theorem IsSeqCompact.isCompact (hs : IsSeqCompact s) : IsCompact s :=
  isCompact_iff_totallyBounded_isComplete.2 ‚ü®hs.totallyBounded, hs.isComplete‚ü©

/-- A version of Bolzano-Weierstrass: in a uniform space with countably generated uniformity filter
(e.g., in a metric space), a set is compact if and only if it is sequentially compact. -/
protected theorem UniformSpace.isCompact_iff_isSeqCompact : IsCompact s ‚Üî IsSeqCompact s :=
  ‚ü®fun H => H.isSeqCompact, fun H => H.isCompact‚ü©

theorem UniformSpace.compactSpace_iff_seqCompactSpace : CompactSpace X ‚Üî SeqCompactSpace X := by
  simp only [‚Üê isCompact_univ_iff, seqCompactSpace_iff, UniformSpace.isCompact_iff_isSeqCompact]

end UniformSpaceSeqCompact
