/-
Copyright (c) 2022 Felix Weilacher. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Felix Weilacher, Emilie Burgun
-/

import Mathlib.Topology.Separation

#align_import topology.perfect from "leanprover-community/mathlib"@"3905fa80e62c0898131285baab35559fbc4e5cda"

/-!
# Perfect Sets

In this file we define perfect subsets of a topological space, and prove some basic properties,
including a version of the Cantor-Bendixson Theorem.

## Main Definitions

* `Perfect C`: A set `C` is perfect, meaning it is closed and every point of it
  is an accumulation point of itself.
* `PerfectSpace X`: A topological space `X` is perfect if its universe is a perfect set.

## Main Statements

* `Perfect.splitting`: A perfect nonempty set contains two disjoint perfect nonempty subsets.
  The main inductive step in the construction of an embedding from the Cantor space to a
  perfect nonempty complete metric space.
* `exists_countable_union_perfect_of_isClosed`: One version of the **Cantor-Bendixson Theorem**:
  A closed set in a second countable space can be written as the union of a countable set and a
  perfect set.

## Implementation Notes

We do not require perfect sets to be nonempty.

We define a nonstandard predicate, `Preperfect`, which drops the closed-ness requirement
from the definition of perfect. In T1 spaces, this is equivalent to having a perfect closure,
see `preperfect_iff_perfect_closure`.

## See also

`Mathlib.Topology.MetricSpace.Perfect`, for properties of perfect sets in metric spaces,
namely Polish spaces.

## References

* [kechris1995] (Chapters 6-7)

## Tags

accumulation point, perfect set, cantor-bendixson.

-/


open Topology Filter Set
open TopologicalSpace (IsTopologicalBasis)
variable {Î± : Type*} [TopologicalSpace Î±] {s t : Set Î±}

section Defs

/-- A set `C` is preperfect if all of its points are accumulation points of itself.
If `C` is nonempty and `Î±` is a T1 space, this is equivalent to the closure of `C` being perfect.
See `preperfect_iff_perfect_closure`.-/
def Preperfect (C : Set Î±) : Prop :=
  âˆ€ â¦ƒxâ¦„, x âˆˆ C â†’ AccPt x (ğ“Ÿ C)
#align preperfect Preperfect

/-- A set `C` is called perfect if it is closed and all of its
points are accumulation points of itself.
Note that we do not require `C` to be nonempty.-/
structure Perfect (C : Set Î±) : Prop where
  closed : IsClosed C
  acc : Preperfect C
#align perfect Perfect

theorem preperfect_iff_nhds : Preperfect s â†” âˆ€ x âˆˆ s, âˆ€ U âˆˆ ğ“ x, âˆƒ y âˆˆ U âˆ© s, y â‰  x := by
  simp only [Preperfect, accPt_iff_nhds]
#align preperfect_iff_nhds preperfect_iff_nhds

/--
A topological space `X` is said to be perfect if its universe is a perfect set.
Equivalently, this means that `ğ“[â‰ ] x â‰  âŠ¥` for every point `x : X`.
-/
class PerfectSpace (X : Type*) [TopologicalSpace X]: Prop :=
  univ_perfect' : Perfect (Set.univ : Set X)

variable [PerfectSpace Î±] in
variable (Î±) in
theorem PerfectSpace.univ_perfect : Perfect (Set.univ : Set Î±) := PerfectSpace.univ_perfect'

end Defs

section Preperfect

/-- If `x` is an accumulation point of a set `C` and `U` is a neighborhood of `x`,
then `x` is an accumulation point of `U âˆ© C`. -/
theorem accPt_principal_iff_inter_of_mem_nhds {x : Î±} (t_nhds : t âˆˆ ğ“ x) :
    AccPt x (ğ“Ÿ s) â†” AccPt x (ğ“Ÿ (s âˆ© t)) := by
  refine âŸ¨fun h_acc => ?acc_inter,
    fun h_acc => AccPt.mono h_acc <| Filter.principal_mono.mpr <| Set.inter_subset_left _ _âŸ©
  have : ğ“[â‰ ] x â‰¤ ğ“Ÿ t := le_principal_iff.mpr <| mem_nhdsWithin_of_mem_nhds t_nhds
  rw [AccPt, â† inf_principal, inf_comm (a := ğ“Ÿ s), â† inf_assoc, inf_of_le_left this]
  exact h_acc

/-- The intersection of a preperfect set and an open set is preperfect. -/
theorem Preperfect.open_inter (s_prePerfect : Preperfect s) (t_open : IsOpen t) :
    Preperfect (s âˆ© t) := fun _ âŸ¨x_in_s, x_in_tâŸ© =>
  (accPt_principal_iff_inter_of_mem_nhds <| t_open.mem_nhds x_in_t).mp (s_prePerfect x_in_s)

#align preperfect.open_inter Preperfect.open_inter

/-- The closure of a preperfect set is perfect.
For a converse, see `preperfect_iff_perfect_closure`. -/
theorem Preperfect.perfect_closure (s_prePerfect : Preperfect s) : Perfect (closure s) := by
  constructor; Â· exact isClosed_closure
  intro x hx
  by_cases h : x âˆˆ s <;> apply AccPt.mono _ (principal_mono.mpr subset_closure)
  Â· exact s_prePerfect h
  have : {x}á¶œ âˆ© s = s := by simp [h]
  rw [AccPt, nhdsWithin, inf_assoc, inf_principal, this]
  rw [closure_eq_cluster_pts] at hx
  exact hx
#align preperfect.perfect_closure Preperfect.perfect_closure

/-- In a T1 space, being preperfect is equivalent to having perfect closure.-/
theorem preperfect_iff_perfect_closure [T1Space Î±] : Preperfect s â†” Perfect (closure s) := by
  constructor <;> intro h
  Â· exact h.perfect_closure
  intro x xC
  have H : AccPt x (ğ“Ÿ (closure s)) := h.acc (subset_closure xC)
  rw [accPt_iff_frequently] at *
  have : âˆ€ y, y â‰  x âˆ§ y âˆˆ closure s â†’ âˆƒá¶  z in ğ“ y, z â‰  x âˆ§ z âˆˆ s := by
    rintro y âŸ¨hyx, yCâŸ©
    simp only [â† mem_compl_singleton_iff, and_comm, â† frequently_nhdsWithin_iff,
      hyx.nhdsWithin_compl_singleton, â† mem_closure_iff_frequently]
    exact yC
  rw [â† frequently_frequently_nhds]
  exact H.mono this
#align preperfect_iff_perfect_closure preperfect_iff_perfect_closure

end Preperfect

section Splitting

theorem Perfect.closure_nhds_inter (s_perfect : Perfect s) (x : Î±) (x_in_s : x âˆˆ s) (x_in_t : x âˆˆ t)
    (t_open : IsOpen t) : Perfect (closure (t âˆ© s)) âˆ§ (closure (t âˆ© s)).Nonempty := âŸ¨
  Preperfect.perfect_closure <| Set.inter_comm _ _ â–¸ s_perfect.acc.open_inter t_open,
  âŸ¨x, subset_closure âŸ¨x_in_t, x_in_sâŸ©âŸ©âŸ©
#align perfect.closure_nhds_inter Perfect.closure_nhds_inter

/-- Given a perfect nonempty set in a T2.5 space, we can find two disjoint perfect subsets.
This is the main inductive step in the proof of the Cantor-Bendixson Theorem. -/
theorem Perfect.splitting [T25Space Î±] (hC : Perfect s) (hnonempty : s.Nonempty) :
    âˆƒ Câ‚€ Câ‚ : Set Î±,
    (Perfect Câ‚€ âˆ§ Câ‚€.Nonempty âˆ§ Câ‚€ âŠ† s) âˆ§ (Perfect Câ‚ âˆ§ Câ‚.Nonempty âˆ§ Câ‚ âŠ† s) âˆ§ Disjoint Câ‚€ Câ‚ := by
  cases' hnonempty with y yC
  obtain âŸ¨x, xC, hxyâŸ© : âˆƒ x âˆˆ s, x â‰  y := by
    have := hC.acc yC
    rw [accPt_iff_nhds] at this
    rcases this univ univ_mem with âŸ¨x, xC, hxyâŸ©
    exact âŸ¨x, xC.2, hxyâŸ©
  obtain âŸ¨U, xU, Uop, V, yV, Vop, hUVâŸ© := exists_open_nhds_disjoint_closure hxy
  use closure (U âˆ© s), closure (V âˆ© s)
  constructor <;> rw [â† and_assoc]
  Â· refine' âŸ¨hC.closure_nhds_inter x xC xU Uop, _âŸ©
    rw [hC.closed.closure_subset_iff]
    exact inter_subset_right _ _
  constructor
  Â· refine' âŸ¨hC.closure_nhds_inter y yC yV Vop, _âŸ©
    rw [hC.closed.closure_subset_iff]
    exact inter_subset_right _ _
  apply Disjoint.mono _ _ hUV <;> apply closure_mono <;> exact inter_subset_left _ _
#align perfect.splitting Perfect.splitting

end Splitting

section Kernel

/-- The **Cantor-Bendixson Theorem**: Any closed subset of a second countable space
can be written as the union of a countable set and a perfect set.-/
theorem exists_countable_union_perfect_of_isClosed [SecondCountableTopology Î±]
    (hclosed : IsClosed s) : âˆƒ V D : Set Î±, V.Countable âˆ§ Perfect D âˆ§ s = V âˆª D := by
  obtain âŸ¨b, bct, _, bbasisâŸ© := TopologicalSpace.exists_countable_basis Î±
  let v := { U âˆˆ b | (U âˆ© s).Countable }
  let V := â‹ƒ U âˆˆ v, U
  let D := s \ V
  have Vct : (V âˆ© s).Countable := by
    simp only [iUnion_inter, mem_sep_iff]
    apply Countable.biUnion
    Â· exact Countable.mono (inter_subset_left _ _) bct
    Â· exact inter_subset_right _ _
  refine' âŸ¨V âˆ© s, D, Vct, âŸ¨_, _âŸ©, _âŸ©
  Â· refine' hclosed.sdiff (isOpen_biUnion fun _ â†¦ _)
    exact fun âŸ¨Ub, _âŸ© â†¦ IsTopologicalBasis.isOpen bbasis Ub
  Â· rw [preperfect_iff_nhds]
    intro x xD E xE
    have : Â¬(E âˆ© D).Countable := by
      intro h
      obtain âŸ¨U, hUb, xU, hUâŸ© : âˆƒ U âˆˆ b, x âˆˆ U âˆ§ U âŠ† E :=
        (IsTopologicalBasis.mem_nhds_iff bbasis).mp xE
      have hU_cnt : (U âˆ© s).Countable := by
        apply @Countable.mono _ _ (E âˆ© D âˆª V âˆ© s)
        Â· rintro y âŸ¨yU, yCâŸ©
          by_cases h : y âˆˆ V
          Â· exact mem_union_right _ (mem_inter h yC)
          Â· exact mem_union_left _ (mem_inter (hU yU) âŸ¨yC, hâŸ©)
        exact Countable.union h Vct
      have : U âˆˆ v := âŸ¨hUb, hU_cntâŸ©
      apply xD.2
      exact mem_biUnion this xU
    by_contra! h
    exact absurd (Countable.mono h (Set.countable_singleton _)) this
  Â· rw [inter_comm, inter_union_diff]
#align exists_countable_union_perfect_of_is_closed exists_countable_union_perfect_of_isClosed

/-- Any uncountable closed set in a second countable space contains a nonempty perfect subset.-/
theorem exists_perfect_nonempty_of_isClosed_of_not_countable [SecondCountableTopology Î±]
    (hclosed : IsClosed s) (hunc : Â¬s.Countable) : âˆƒ D : Set Î±, Perfect D âˆ§ D.Nonempty âˆ§ D âŠ† s := by
  rcases exists_countable_union_perfect_of_isClosed hclosed with âŸ¨V, D, Vct, Dperf, VDâŸ©
  refine' âŸ¨D, âŸ¨Dperf, _âŸ©âŸ©
  constructor
  Â· rw [nonempty_iff_ne_empty]
    by_contra h
    rw [h, union_empty] at VD
    rw [VD] at hunc
    contradiction
  rw [VD]
  exact subset_union_right _ _
#align exists_perfect_nonempty_of_is_closed_of_not_countable exists_perfect_nonempty_of_isClosed_of_not_countable

end Kernel

section PerfectSpace

theorem perfectSpace_of_forall_not_isolated (h_forall : âˆ€ x : Î±, Filter.NeBot (ğ“[â‰ ] x)) :
    PerfectSpace Î± := âŸ¨âŸ¨isClosed_univ, fun x _ => by
  rw [AccPt, Filter.principal_univ, inf_top_eq]
  exact h_forall xâŸ©âŸ©

variable [PerfectSpace Î±]

instance PerfectSpace.not_isolated (x : Î±): Filter.NeBot (ğ“[â‰ ] x) := by
  have := (PerfectSpace.univ_perfect Î±).acc (Set.mem_univ x)
  rwa [AccPt, Filter.principal_univ, inf_top_eq] at this

end PerfectSpace

section PerfectSpace.Prod

variable {Î² : Type*} [TopologicalSpace Î²]

theorem nhdsWithin_punctured_prod_neBot_iff' {p : Î±} {q : Î²} : Filter.NeBot (ğ“[â‰ ] (p, q)) â†”
    Filter.NeBot (ğ“[â‰ ] p) âˆ¨ Filter.NeBot (ğ“[â‰ ] q) := by
  simp_rw [â† Set.singleton_prod_singleton, Set.compl_prod_eq_union, nhdsWithin_union,
    nhdsWithin_prod_eq, nhdsWithin_univ, Filter.neBot_iff, ne_eq, sup_eq_bot_iff,
    Filter.prod_eq_bot, Filter.NeBot.ne <| nhds_neBot, or_false, false_or, not_and_or]

variable (Î± Î²) in
instance PerfectSpace.prod_left [PerfectSpace Î±] : PerfectSpace (Î± Ã— Î²) :=
  perfectSpace_of_forall_not_isolated fun âŸ¨p, qâŸ© => by
    rw [nhdsWithin_punctured_prod_neBot_iff]
    left
    exact PerfectSpace.not_isolated p

variable (Î± Î²) in
instance PerfectSpace.prod_right [PerfectSpace Î²] : PerfectSpace (Î± Ã— Î²) :=
  perfectSpace_of_forall_not_isolated fun âŸ¨p, qâŸ© => by
    rw [nhdsWithin_punctured_prod_neBot_iff]
    right
    exact PerfectSpace.not_isolated q

end PerfectSpace.Prod

@[deprecated accPt_principal_iff_inter_of_mem_nhds]
theorem AccPt.nhds_inter {x : Î±} (h_acc : AccPt x (ğ“Ÿ s)) (t_nhds : t âˆˆ ğ“ x) :
    AccPt x (ğ“Ÿ (t âˆ© s)) :=
  Set.inter_comm _ _ â–¸ (accPt_principal_iff_inter_of_mem_nhds t_nhds).mp h_acc
#align acc_pt.nhds_inter AccPt.nhds_inter
