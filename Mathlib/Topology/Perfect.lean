/-
Copyright (c) 2022 Felix Weilacher. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Felix Weilacher
-/

import Mathlib.Topology.Separation.Regular

/-!
# Perfect Sets

In this file we define perfect subsets of a topological space, and prove some basic properties,
including a version of the Cantor-Bendixson Theorem.

## Main Definitions

* `Preperfect C`: A set `C` is preperfect if every point of `C` is an accumulation point
  of `C`. Equivalently, if it has no isolated points in the induced topology.
  This property is also called dense-in-itself.
* `Perfect C`: A set `C` is perfect, meaning it is closed and every point of it
  is an accumulation point of itself.
* `PerfectSpace X`: A topological space `X` is perfect if its universe is a perfect set.

## Main Statements

* `preperfect_iff_perfect_closure`: In a T1 space, a set is preperfect iff its closure is perfect.
* `Perfect.splitting`: A perfect nonempty set contains two disjoint perfect nonempty subsets.
  The main inductive step in the construction of an embedding from the Cantor space to a
  perfect nonempty complete metric space.
* `exists_countable_union_perfect_of_isClosed`: One version of the **Cantor-Bendixson Theorem**:
  A closed set in a second countable space can be written as the union of a countable set and a
  perfect set.

## Implementation Notes

We do not require perfect sets to be nonempty.

## See also

`Mathlib/Topology/MetricSpace/Perfect.lean`, for properties of perfect sets in metric spaces,
namely Polish spaces.

## References

* [kechris1995] (Chapters 6-7)

## Tags

accumulation point, perfect set, dense-in-itself, cantor-bendixson.

-/


open Topology Filter Set TopologicalSpace

section Basic

variable {Î± : Type*} [TopologicalSpace Î±] {C : Set Î±}

/-- If `x` is an accumulation point of a set `C` and `U` is a neighborhood of `x`,
then `x` is an accumulation point of `U âˆ© C`. -/
theorem AccPt.nhds_inter {x : Î±} {U : Set Î±} (h_acc : AccPt x (ğ“Ÿ C)) (hU : U âˆˆ ğ“ x) :
    AccPt x (ğ“Ÿ (U âˆ© C)) := by
  have : ğ“[â‰ ] x â‰¤ ğ“Ÿ U := by
    rw [le_principal_iff]
    exact mem_nhdsWithin_of_mem_nhds hU
  rw [AccPt, â† inf_principal, â† inf_assoc, inf_of_le_left this]
  exact h_acc

/-- A set `C` is preperfect if all of its points are accumulation points of itself.
If `Î±` is a T1 space, this is equivalent to the closure of `C` being perfect,
see `preperfect_iff_perfect_closure`. This property is also called dense-in-itself. -/
def Preperfect (C : Set Î±) : Prop :=
  âˆ€ x âˆˆ C, AccPt x (ğ“Ÿ C)

/-- A set `C` is called perfect if it is closed and all of its
points are accumulation points of itself.
Note that we do not require `C` to be nonempty. -/
@[mk_iff perfect_def]
structure Perfect (C : Set Î±) : Prop where
  closed : IsClosed C
  acc : Preperfect C

theorem preperfect_iff_nhds : Preperfect C â†” âˆ€ x âˆˆ C, âˆ€ U âˆˆ ğ“ x, âˆƒ y âˆˆ U âˆ© C, y â‰  x := by
  simp only [Preperfect, accPt_iff_nhds]

section PerfectSpace

variable (Î±)

/--
A topological space `X` is said to be perfect if its universe is a perfect set.
Equivalently, this means that `ğ“[â‰ ] x â‰  âŠ¥` for every point `x : X`.
-/
@[mk_iff perfectSpace_def]
class PerfectSpace : Prop where
  univ_preperfect : Preperfect (Set.univ : Set Î±)

theorem PerfectSpace.univ_perfect [PerfectSpace Î±] : Perfect (Set.univ : Set Î±) :=
  âŸ¨isClosed_univ, PerfectSpace.univ_preperfectâŸ©

end PerfectSpace

section Preperfect

/-- The intersection of a preperfect set and an open set is preperfect. -/
theorem Preperfect.open_inter {U : Set Î±} (hC : Preperfect C) (hU : IsOpen U) :
    Preperfect (U âˆ© C) := by
  rintro x âŸ¨xU, xCâŸ©
  apply (hC _ xC).nhds_inter
  exact hU.mem_nhds xU

/-- The closure of a preperfect set is perfect.
For a converse, see `preperfect_iff_perfect_closure`. -/
theorem Preperfect.perfect_closure (hC : Preperfect C) : Perfect (closure C) := by
  constructor; Â· exact isClosed_closure
  intro x hx
  by_cases h : x âˆˆ C <;> apply AccPt.mono _ (principal_mono.mpr subset_closure)
  Â· exact hC _ h
  have : {x}á¶œ âˆ© C = C := by simp [h]
  rw [AccPt, nhdsWithin, inf_assoc, inf_principal, this]
  rw [closure_eq_cluster_pts] at hx
  exact hx

/-- In a T1 space, being preperfect is equivalent to having perfect closure. -/
theorem preperfect_iff_perfect_closure [T1Space Î±] : Preperfect C â†” Perfect (closure C) := by
  constructor <;> intro h
  Â· exact h.perfect_closure
  intro x xC
  have H : AccPt x (ğ“Ÿ (closure C)) := h.acc _ (subset_closure xC)
  rw [accPt_iff_frequently] at *
  have : âˆ€ y, y â‰  x âˆ§ y âˆˆ closure C â†’ âˆƒá¶  z in ğ“ y, z â‰  x âˆ§ z âˆˆ C := by
    rintro y âŸ¨hyx, yCâŸ©
    simp only [â† mem_compl_singleton_iff, and_comm, â† frequently_nhdsWithin_iff,
      hyx.nhdsWithin_compl_singleton, â† mem_closure_iff_frequently]
    exact yC
  rw [â† frequently_frequently_nhds]
  exact H.mono this

theorem Perfect.closure_nhds_inter {U : Set Î±} (hC : Perfect C) (x : Î±) (xC : x âˆˆ C) (xU : x âˆˆ U)
    (Uop : IsOpen U) : Perfect (closure (U âˆ© C)) âˆ§ (closure (U âˆ© C)).Nonempty := by
  constructor
  Â· apply Preperfect.perfect_closure
    exact hC.acc.open_inter Uop
  apply Nonempty.closure
  exact âŸ¨x, âŸ¨xU, xCâŸ©âŸ©

/-- Given a perfect nonempty set in a T2.5 space, we can find two disjoint perfect subsets.
This is the main inductive step in the proof of the Cantor-Bendixson Theorem. -/
theorem Perfect.splitting [T25Space Î±] (hC : Perfect C) (hnonempty : C.Nonempty) :
    âˆƒ Câ‚€ Câ‚ : Set Î±,
    (Perfect Câ‚€ âˆ§ Câ‚€.Nonempty âˆ§ Câ‚€ âŠ† C) âˆ§ (Perfect Câ‚ âˆ§ Câ‚.Nonempty âˆ§ Câ‚ âŠ† C) âˆ§ Disjoint Câ‚€ Câ‚ := by
  obtain âŸ¨y, yCâŸ© := hnonempty
  obtain âŸ¨x, xC, hxyâŸ© : âˆƒ x âˆˆ C, x â‰  y := by
    have := hC.acc _ yC
    rw [accPt_iff_nhds] at this
    rcases this univ univ_mem with âŸ¨x, xC, hxyâŸ©
    exact âŸ¨x, xC.2, hxyâŸ©
  obtain âŸ¨U, xU, Uop, V, yV, Vop, hUVâŸ© := exists_open_nhds_disjoint_closure hxy
  use closure (U âˆ© C), closure (V âˆ© C)
  constructor <;> rw [â† and_assoc]
  Â· refine âŸ¨hC.closure_nhds_inter x xC xU Uop, ?_âŸ©
    rw [hC.closed.closure_subset_iff]
    exact inter_subset_right
  constructor
  Â· refine âŸ¨hC.closure_nhds_inter y yC yV Vop, ?_âŸ©
    rw [hC.closed.closure_subset_iff]
    exact inter_subset_right
  apply Disjoint.mono _ _ hUV <;> apply closure_mono <;> exact inter_subset_left

lemma IsPreconnected.preperfect_of_nontrivial [T1Space Î±] {U : Set Î±} (hu : U.Nontrivial)
    (h : IsPreconnected U) : Preperfect U := by
  intro x hx
  rw [isPreconnected_closed_iff] at h
  specialize h {x} (closure (U \ {x})) isClosed_singleton isClosed_closure ?_ ?_ ?_
  Â· trans {x} âˆª (U \ {x})
    Â· simp
    apply Set.union_subset_union_right
    exact subset_closure
  Â· exact Set.inter_singleton_nonempty.mpr hx
  Â· obtain âŸ¨y, hyâŸ© := Set.Nontrivial.exists_ne hu x
    use y
    simp only [Set.mem_inter_iff, hy, true_and]
    apply subset_closure
    simp [hy]
  Â· apply Set.Nonempty.right at h
    rw [Set.singleton_inter_nonempty, mem_closure_iff_clusterPt,
      â† accPt_principal_iff_clusterPt] at h
    exact h

end Preperfect

section Kernel

/-- The **Cantor-Bendixson Theorem**: Any closed subset of a second countable space
can be written as the union of a countable set and a perfect set. -/
theorem exists_countable_union_perfect_of_isClosed [SecondCountableTopology Î±]
    (hclosed : IsClosed C) : âˆƒ V D : Set Î±, V.Countable âˆ§ Perfect D âˆ§ C = V âˆª D := by
  obtain âŸ¨b, bct, _, bbasisâŸ© := TopologicalSpace.exists_countable_basis Î±
  let v := { U âˆˆ b | (U âˆ© C).Countable }
  let V := â‹ƒ U âˆˆ v, U
  let D := C \ V
  have Vct : (V âˆ© C).Countable := by
    simp only [V, iUnion_inter]
    apply Countable.biUnion
    Â· exact Countable.mono inter_subset_left bct
    Â· exact inter_subset_right
  refine âŸ¨V âˆ© C, D, Vct, âŸ¨?_, ?_âŸ©, ?_âŸ©
  Â· refine hclosed.sdiff (isOpen_biUnion fun _ â†¦ ?_)
    exact fun âŸ¨Ub, _âŸ© â†¦ IsTopologicalBasis.isOpen bbasis Ub
  Â· rw [preperfect_iff_nhds]
    intro x xD E xE
    have : Â¬(E âˆ© D).Countable := by
      intro h
      obtain âŸ¨U, hUb, xU, hUâŸ© : âˆƒ U âˆˆ b, x âˆˆ U âˆ§ U âŠ† E :=
        (IsTopologicalBasis.mem_nhds_iff bbasis).mp xE
      have hU_cnt : (U âˆ© C).Countable := by
        apply @Countable.mono _ _ (E âˆ© D âˆª V âˆ© C)
        Â· rintro y âŸ¨yU, yCâŸ©
          by_cases h : y âˆˆ V
          Â· exact mem_union_right _ (mem_inter h yC)
          Â· exact mem_union_left _ (mem_inter (hU yU) âŸ¨yC, hâŸ©)
        exact Countable.union h Vct
      have : U âˆˆ v := âŸ¨hUb, hU_cntâŸ©
      apply xD.2
      exact mem_biUnion this xU
    by_contra! h
    exact absurd (Countable.mono h (Set.countable_singleton _)) this
  Â· rw [inter_comm, inter_union_diff]

/-- Any uncountable closed set in a second countable space contains a nonempty perfect subset. -/
theorem exists_perfect_nonempty_of_isClosed_of_not_countable [SecondCountableTopology Î±]
    (hclosed : IsClosed C) (hunc : Â¬C.Countable) : âˆƒ D : Set Î±, Perfect D âˆ§ D.Nonempty âˆ§ D âŠ† C := by
  rcases exists_countable_union_perfect_of_isClosed hclosed with âŸ¨V, D, Vct, Dperf, VDâŸ©
  refine âŸ¨D, âŸ¨Dperf, ?_âŸ©âŸ©
  constructor
  Â· rw [nonempty_iff_ne_empty]
    by_contra h
    rw [h, union_empty] at VD
    rw [VD] at hunc
    contradiction
  rw [VD]
  exact subset_union_right

end Kernel

end Basic

section PerfectSpace

variable {X : Type*} [TopologicalSpace X]

theorem perfectSpace_iff_forall_not_isolated : PerfectSpace X â†” âˆ€ x : X, Filter.NeBot (ğ“[â‰ ] x) := by
  simp [perfectSpace_def, Preperfect, AccPt]

instance PerfectSpace.not_isolated [PerfectSpace X] (x : X) : Filter.NeBot (ğ“[â‰ ] x) :=
  perfectSpace_iff_forall_not_isolated.mp â€¹_â€º x

end PerfectSpace
