/-
Copyright (c) 2022 Felix Weilacher. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Felix Weilacher
-/
import Mathlib.Topology.MetricSpace.Polish
import Mathlib.Topology.MetricSpace.CantorScheme

#align_import topology.perfect from "leanprover-community/mathlib"@"3905fa80e62c0898131285baab35559fbc4e5cda"

/-!
# Perfect Sets

In this file we define perfect subsets of a topological space, and prove some basic properties,
including a version of the Cantor-Bendixson Theorem.

## Main Definitions

* `Perfect C`: A set `C` is perfect, meaning it is closed and every point of it
  is an accumulation point of itself.

## Main Statements

* `Perfect.splitting`: A perfect nonempty set contains two disjoint perfect nonempty subsets.
  The main inductive step in the construction of an embedding from the Cantor space to a
  perfect nonempty complete metric space.
* `exists_countable_union_perfect_of_isClosed`: One version of the **Cantor-Bendixson Theorem**:
  A closed set in a second countable space can be written as the union of a countable set and a
  perfect set.
* `Perfect.exists_nat_bool_injection`: A perfect nonempty set in a complete metric space
  admits an embedding from the Cantor space.

## Implementation Notes

We do not require perfect sets to be nonempty.

We define a nonstandard predicate, `Preperfect`, which drops the closed-ness requirement
from the definition of perfect. In T1 spaces, this is equivalent to having a perfect closure,
see `preperfect_iff_perfect_closure`.

## References

* [kechris1995] (Chapters 6-7)

## Tags

accumulation point, perfect set, cantor-bendixson.

-/


open Topology Filter

open TopologicalSpace Filter Set

section Basic

variable {Î± : Type*} [TopologicalSpace Î±] {C : Set Î±}

/-- If `x` is an accumulation point of a set `C` and `U` is a neighborhood of `x`,
then `x` is an accumulation point of `U âˆ© C`. -/
theorem AccPt.nhds_inter {x : Î±} {U : Set Î±} (h_acc : AccPt x (ğ“Ÿ C)) (hU : U âˆˆ ğ“ x) :
    AccPt x (ğ“Ÿ (U âˆ© C)) := by
  have : ğ“[â‰ ] x â‰¤ ğ“Ÿ U := by
    rw [le_principal_iff]
    exact mem_nhdsWithin_of_mem_nhds hU
  rw [AccPt, â† inf_principal, â† inf_assoc, inf_of_le_left this]
  -- âŠ¢ NeBot (ğ“[{x}á¶œ] x âŠ“ ğ“Ÿ C)
  exact h_acc
  -- ğŸ‰ no goals
#align acc_pt.nhds_inter AccPt.nhds_inter

/-- A set `C` is preperfect if all of its points are accumulation points of itself.
If `C` is nonempty and `Î±` is a T1 space, this is equivalent to the closure of `C` being perfect.
See `preperfect_iff_perfect_closure`.-/
def Preperfect (C : Set Î±) : Prop :=
  âˆ€ x âˆˆ C, AccPt x (ğ“Ÿ C)
#align preperfect Preperfect

/-- A set `C` is called perfect if it is closed and all of its
points are accumulation points of itself.
Note that we do not require `C` to be nonempty.-/
structure Perfect (C : Set Î±) : Prop where
  closed : IsClosed C
  acc : Preperfect C
#align perfect Perfect

theorem preperfect_iff_nhds : Preperfect C â†” âˆ€ x âˆˆ C, âˆ€ U âˆˆ ğ“ x, âˆƒ y âˆˆ U âˆ© C, y â‰  x := by
  simp only [Preperfect, accPt_iff_nhds]
  -- ğŸ‰ no goals
#align preperfect_iff_nhds preperfect_iff_nhds

/-- The intersection of a preperfect set and an open set is preperfect. -/
theorem Preperfect.open_inter {U : Set Î±} (hC : Preperfect C) (hU : IsOpen U) :
    Preperfect (U âˆ© C) := by
  rintro x âŸ¨xU, xCâŸ©
  -- âŠ¢ AccPt x (ğ“Ÿ (U âˆ© C))
  apply (hC _ xC).nhds_inter
  -- âŠ¢ U âˆˆ ğ“ x
  exact hU.mem_nhds xU
  -- ğŸ‰ no goals
#align preperfect.open_inter Preperfect.open_inter

/-- The closure of a preperfect set is perfect.
For a converse, see `preperfect_iff_perfect_closure`. -/
theorem Preperfect.perfect_closure (hC : Preperfect C) : Perfect (closure C) := by
  constructor; Â· exact isClosed_closure
  -- âŠ¢ IsClosed (closure C)
                 -- ğŸ‰ no goals
  intro x hx
  -- âŠ¢ AccPt x (ğ“Ÿ (closure C))
  by_cases h : x âˆˆ C <;> apply AccPt.mono _ (principal_mono.mpr subset_closure)
  -- âŠ¢ AccPt x (ğ“Ÿ (closure C))
                         -- âŠ¢ AccPt x (ğ“Ÿ C)
                         -- âŠ¢ AccPt x (ğ“Ÿ C)
  Â· exact hC _ h
    -- ğŸ‰ no goals
  have : {x}á¶œ âˆ© C = C := by simp [h]
  -- âŠ¢ AccPt x (ğ“Ÿ C)
  rw [AccPt, nhdsWithin, inf_assoc, inf_principal, this]
  -- âŠ¢ NeBot (ğ“ x âŠ“ ğ“Ÿ C)
  rw [closure_eq_cluster_pts] at hx
  -- âŠ¢ NeBot (ğ“ x âŠ“ ğ“Ÿ C)
  exact hx
  -- ğŸ‰ no goals
#align preperfect.perfect_closure Preperfect.perfect_closure

/-- In a T1 space, being preperfect is equivalent to having perfect closure.-/
theorem preperfect_iff_perfect_closure [T1Space Î±] : Preperfect C â†” Perfect (closure C) := by
  constructor <;> intro h
  -- âŠ¢ Preperfect C â†’ Perfect (closure C)
                  -- âŠ¢ Perfect (closure C)
                  -- âŠ¢ Preperfect C
  Â· exact h.perfect_closure
    -- ğŸ‰ no goals
  intro x xC
  -- âŠ¢ AccPt x (ğ“Ÿ C)
  have H : AccPt x (ğ“Ÿ (closure C)) := h.acc _ (subset_closure xC)
  -- âŠ¢ AccPt x (ğ“Ÿ C)
  rw [accPt_iff_frequently] at *
  -- âŠ¢ âˆƒá¶  (y : Î±) in ğ“ x, y â‰  x âˆ§ y âˆˆ C
  have : âˆ€ y, y â‰  x âˆ§ y âˆˆ closure C â†’ âˆƒá¶  z in ğ“ y, z â‰  x âˆ§ z âˆˆ C := by
    rintro y âŸ¨hyx, yCâŸ©
    simp only [â† mem_compl_singleton_iff, and_comm, â† frequently_nhdsWithin_iff,
      hyx.nhdsWithin_compl_singleton, â† mem_closure_iff_frequently]
    exact yC
  rw [â† frequently_frequently_nhds]
  -- âŠ¢ âˆƒá¶  (y : Î±) in ğ“ x, âˆƒá¶  (x_1 : Î±) in ğ“ y, x_1 â‰  x âˆ§ x_1 âˆˆ C
  exact H.mono this
  -- ğŸ‰ no goals
#align preperfect_iff_perfect_closure preperfect_iff_perfect_closure

theorem Perfect.closure_nhds_inter {U : Set Î±} (hC : Perfect C) (x : Î±) (xC : x âˆˆ C) (xU : x âˆˆ U)
    (Uop : IsOpen U) : Perfect (closure (U âˆ© C)) âˆ§ (closure (U âˆ© C)).Nonempty := by
  constructor
  -- âŠ¢ Perfect (closure (U âˆ© C))
  Â· apply Preperfect.perfect_closure
    -- âŠ¢ Preperfect (U âˆ© C)
    exact hC.acc.open_inter Uop
    -- ğŸ‰ no goals
  apply Nonempty.closure
  -- âŠ¢ Set.Nonempty (U âˆ© C)
  exact âŸ¨x, âŸ¨xU, xCâŸ©âŸ©
  -- ğŸ‰ no goals
#align perfect.closure_nhds_inter Perfect.closure_nhds_inter

/-- Given a perfect nonempty set in a T2.5 space, we can find two disjoint perfect subsets.
This is the main inductive step in the proof of the Cantor-Bendixson Theorem. -/
theorem Perfect.splitting [T25Space Î±] (hC : Perfect C) (hnonempty : C.Nonempty) :
    âˆƒ Câ‚€ Câ‚ : Set Î±,
    (Perfect Câ‚€ âˆ§ Câ‚€.Nonempty âˆ§ Câ‚€ âŠ† C) âˆ§ (Perfect Câ‚ âˆ§ Câ‚.Nonempty âˆ§ Câ‚ âŠ† C) âˆ§ Disjoint Câ‚€ Câ‚ := by
  cases' hnonempty with y yC
  -- âŠ¢ âˆƒ Câ‚€ Câ‚, (Perfect Câ‚€ âˆ§ Set.Nonempty Câ‚€ âˆ§ Câ‚€ âŠ† C) âˆ§ (Perfect Câ‚ âˆ§ Set.Nonempt â€¦
  obtain âŸ¨x, xC, hxyâŸ© : âˆƒ x âˆˆ C, x â‰  y := by
    have := hC.acc _ yC
    rw [accPt_iff_nhds] at this
    rcases this univ univ_mem with âŸ¨x, xC, hxyâŸ©
    exact âŸ¨x, xC.2, hxyâŸ©
  obtain âŸ¨U, xU, Uop, V, yV, Vop, hUVâŸ© := exists_open_nhds_disjoint_closure hxy
  -- âŠ¢ âˆƒ Câ‚€ Câ‚, (Perfect Câ‚€ âˆ§ Set.Nonempty Câ‚€ âˆ§ Câ‚€ âŠ† C) âˆ§ (Perfect Câ‚ âˆ§ Set.Nonempt â€¦
  use closure (U âˆ© C), closure (V âˆ© C)
  -- âŠ¢ (Perfect (closure (U âˆ© C)) âˆ§ Set.Nonempty (closure (U âˆ© C)) âˆ§ closure (U âˆ© C â€¦
  constructor <;> rw [â† and_assoc]
  -- âŠ¢ Perfect (closure (U âˆ© C)) âˆ§ Set.Nonempty (closure (U âˆ© C)) âˆ§ closure (U âˆ© C) â€¦
                  -- âŠ¢ (Perfect (closure (U âˆ© C)) âˆ§ Set.Nonempty (closure (U âˆ© C))) âˆ§ closure (U âˆ©  â€¦
                  -- âŠ¢ ((Perfect (closure (V âˆ© C)) âˆ§ Set.Nonempty (closure (V âˆ© C))) âˆ§ closure (V âˆ© â€¦
  Â· refine' âŸ¨hC.closure_nhds_inter x xC xU Uop, _âŸ©
    -- âŠ¢ closure (U âˆ© C) âŠ† C
    rw [hC.closed.closure_subset_iff]
    -- âŠ¢ U âˆ© C âŠ† C
    exact inter_subset_right _ _
    -- ğŸ‰ no goals
  constructor
  -- âŠ¢ (Perfect (closure (V âˆ© C)) âˆ§ Set.Nonempty (closure (V âˆ© C))) âˆ§ closure (V âˆ©  â€¦
  Â· refine' âŸ¨hC.closure_nhds_inter y yC yV Vop, _âŸ©
    -- âŠ¢ closure (V âˆ© C) âŠ† C
    rw [hC.closed.closure_subset_iff]
    -- âŠ¢ V âˆ© C âŠ† C
    exact inter_subset_right _ _
    -- ğŸ‰ no goals
  apply Disjoint.mono _ _ hUV <;> apply closure_mono <;> exact inter_subset_left _ _
  -- âŠ¢ closure (U âˆ© C) â‰¤ closure U
                                  -- âŠ¢ U âˆ© C âŠ† U
                                  -- âŠ¢ V âˆ© C âŠ† V
                                                         -- ğŸ‰ no goals
                                                         -- ğŸ‰ no goals
#align perfect.splitting Perfect.splitting

section Kernel

/-- The **Cantor-Bendixson Theorem**: Any closed subset of a second countable space
can be written as the union of a countable set and a perfect set.-/
theorem exists_countable_union_perfect_of_isClosed [SecondCountableTopology Î±]
    (hclosed : IsClosed C) : âˆƒ V D : Set Î±, V.Countable âˆ§ Perfect D âˆ§ C = V âˆª D := by
  obtain âŸ¨b, bct, _, bbasisâŸ© := TopologicalSpace.exists_countable_basis Î±
  -- âŠ¢ âˆƒ V D, Set.Countable V âˆ§ Perfect D âˆ§ C = V âˆª D
  let v := { U âˆˆ b | (U âˆ© C).Countable }
  -- âŠ¢ âˆƒ V D, Set.Countable V âˆ§ Perfect D âˆ§ C = V âˆª D
  let V := â‹ƒ U âˆˆ v, U
  -- âŠ¢ âˆƒ V D, Set.Countable V âˆ§ Perfect D âˆ§ C = V âˆª D
  let D := C \ V
  -- âŠ¢ âˆƒ V D, Set.Countable V âˆ§ Perfect D âˆ§ C = V âˆª D
  have Vct : (V âˆ© C).Countable := by
    simp only [iUnion_inter, mem_sep_iff]
    apply Countable.biUnion
    Â· exact Countable.mono (inter_subset_left _ _) bct
    Â· exact inter_subset_right _ _
  refine' âŸ¨V âˆ© C, D, Vct, âŸ¨_, _âŸ©, _âŸ©
  Â· refine' hclosed.sdiff (isOpen_biUnion fun _ â†¦ _)
    -- âŠ¢ xâœ âˆˆ v â†’ IsOpen xâœ
    exact fun âŸ¨Ub, _âŸ© â†¦ IsTopologicalBasis.isOpen bbasis Ub
    -- ğŸ‰ no goals
  Â· rw [preperfect_iff_nhds]
    -- âŠ¢ âˆ€ (x : Î±), x âˆˆ D â†’ âˆ€ (U : Set Î±), U âˆˆ ğ“ x â†’ âˆƒ y, y âˆˆ U âˆ© D âˆ§ y â‰  x
    intro x xD E xE
    -- âŠ¢ âˆƒ y, y âˆˆ E âˆ© D âˆ§ y â‰  x
    have : Â¬(E âˆ© D).Countable := by
      intro h
      obtain âŸ¨U, hUb, xU, hUâŸ© : âˆƒ U âˆˆ b, x âˆˆ U âˆ§ U âŠ† E :=
        (IsTopologicalBasis.mem_nhds_iff bbasis).mp xE
      have hU_cnt : (U âˆ© C).Countable := by
        apply @Countable.mono _ _ (E âˆ© D âˆª V âˆ© C)
        Â· rintro y âŸ¨yU, yCâŸ©
          by_cases h : y âˆˆ V
          Â· exact mem_union_right _ (mem_inter h yC)
          Â· exact mem_union_left _ (mem_inter (hU yU) âŸ¨yC, hâŸ©)
        exact Countable.union h Vct
      have : U âˆˆ v := âŸ¨hUb, hU_cntâŸ©
      apply xD.2
      exact mem_biUnion this xU
    by_contra' h
    -- âŠ¢ False
    exact absurd (Countable.mono h (Set.countable_singleton _)) this
    -- ğŸ‰ no goals
  Â· rw [inter_comm, inter_union_diff]
    -- ğŸ‰ no goals
#align exists_countable_union_perfect_of_is_closed exists_countable_union_perfect_of_isClosed

/-- Any uncountable closed set in a second countable space contains a nonempty perfect subset.-/
theorem exists_perfect_nonempty_of_isClosed_of_not_countable [SecondCountableTopology Î±]
    (hclosed : IsClosed C) (hunc : Â¬C.Countable) : âˆƒ D : Set Î±, Perfect D âˆ§ D.Nonempty âˆ§ D âŠ† C := by
  rcases exists_countable_union_perfect_of_isClosed hclosed with âŸ¨V, D, Vct, Dperf, VDâŸ©
  -- âŠ¢ âˆƒ D, Perfect D âˆ§ Set.Nonempty D âˆ§ D âŠ† C
  refine' âŸ¨D, âŸ¨Dperf, _âŸ©âŸ©
  -- âŠ¢ Set.Nonempty D âˆ§ D âŠ† C
  constructor
  -- âŠ¢ Set.Nonempty D
  Â· rw [nonempty_iff_ne_empty]
    -- âŠ¢ D â‰  âˆ…
    by_contra h
    -- âŠ¢ False
    rw [h, union_empty] at VD
    -- âŠ¢ False
    rw [VD] at hunc
    -- âŠ¢ False
    contradiction
    -- ğŸ‰ no goals
  rw [VD]
  -- âŠ¢ D âŠ† V âˆª D
  exact subset_union_right _ _
  -- ğŸ‰ no goals
#align exists_perfect_nonempty_of_is_closed_of_not_countable exists_perfect_nonempty_of_isClosed_of_not_countable

end Kernel

end Basic

section CantorInjMetric

open Function ENNReal

variable {Î± : Type*} [MetricSpace Î±] {C : Set Î±} (hC : Perfect C) {Îµ : â„â‰¥0âˆ}

private theorem Perfect.small_diam_aux (Îµ_pos : 0 < Îµ) {x : Î±} (xC : x âˆˆ C) :
    let D := closure (EMetric.ball x (Îµ / 2) âˆ© C)
    Perfect D âˆ§ D.Nonempty âˆ§ D âŠ† C âˆ§ EMetric.diam D â‰¤ Îµ := by
  have : x âˆˆ EMetric.ball x (Îµ / 2) := by
    apply EMetric.mem_ball_self
    rw [ENNReal.div_pos_iff]
    exact âŸ¨ne_of_gt Îµ_pos, by norm_numâŸ©
  have := hC.closure_nhds_inter x xC this EMetric.isOpen_ball
  -- âŠ¢ let D := closure (EMetric.ball x (Îµ / 2) âˆ© C);
  refine' âŸ¨this.1, this.2, _, _âŸ©
  -- âŠ¢ closure (EMetric.ball x (Îµ / 2) âˆ© C) âŠ† C
  Â· rw [IsClosed.closure_subset_iff hC.closed]
    -- âŠ¢ EMetric.ball x (Îµ / 2) âˆ© C âŠ† C
    apply inter_subset_right
    -- ğŸ‰ no goals
  rw [EMetric.diam_closure]
  -- âŠ¢ EMetric.diam (EMetric.ball x (Îµ / 2) âˆ© C) â‰¤ Îµ
  apply le_trans (EMetric.diam_mono (inter_subset_left _ _))
  -- âŠ¢ EMetric.diam (EMetric.ball x (Îµ / 2)) â‰¤ Îµ
  convert EMetric.diam_ball (x := x)
  -- âŠ¢ Îµ = 2 * (Îµ / 2)
  rw [mul_comm, ENNReal.div_mul_cancel] <;> norm_num
  -- âŠ¢ 2 â‰  0
                                            -- ğŸ‰ no goals
                                            -- ğŸ‰ no goals

variable (hnonempty : C.Nonempty)

/-- A refinement of `Perfect.splitting` for metric spaces, where we also control
the diameter of the new perfect sets. -/
theorem Perfect.small_diam_splitting (Îµ_pos : 0 < Îµ) :
    âˆƒ Câ‚€ Câ‚ : Set Î±, (Perfect Câ‚€ âˆ§ Câ‚€.Nonempty âˆ§ Câ‚€ âŠ† C âˆ§ EMetric.diam Câ‚€ â‰¤ Îµ) âˆ§
    (Perfect Câ‚ âˆ§ Câ‚.Nonempty âˆ§ Câ‚ âŠ† C âˆ§ EMetric.diam Câ‚ â‰¤ Îµ) âˆ§ Disjoint Câ‚€ Câ‚ := by
  rcases hC.splitting hnonempty with âŸ¨Dâ‚€, Dâ‚, âŸ¨perf0, non0, sub0âŸ©, âŸ¨perf1, non1, sub1âŸ©, hdisjâŸ©
  -- âŠ¢ âˆƒ Câ‚€ Câ‚, (Perfect Câ‚€ âˆ§ Set.Nonempty Câ‚€ âˆ§ Câ‚€ âŠ† C âˆ§ EMetric.diam Câ‚€ â‰¤ Îµ) âˆ§ (Pe â€¦
  cases' non0 with xâ‚€ hxâ‚€
  -- âŠ¢ âˆƒ Câ‚€ Câ‚, (Perfect Câ‚€ âˆ§ Set.Nonempty Câ‚€ âˆ§ Câ‚€ âŠ† C âˆ§ EMetric.diam Câ‚€ â‰¤ Îµ) âˆ§ (Pe â€¦
  cases' non1 with xâ‚ hxâ‚
  -- âŠ¢ âˆƒ Câ‚€ Câ‚, (Perfect Câ‚€ âˆ§ Set.Nonempty Câ‚€ âˆ§ Câ‚€ âŠ† C âˆ§ EMetric.diam Câ‚€ â‰¤ Îµ) âˆ§ (Pe â€¦
  rcases perf0.small_diam_aux Îµ_pos hxâ‚€ with âŸ¨perf0', non0', sub0', diam0âŸ©
  -- âŠ¢ âˆƒ Câ‚€ Câ‚, (Perfect Câ‚€ âˆ§ Set.Nonempty Câ‚€ âˆ§ Câ‚€ âŠ† C âˆ§ EMetric.diam Câ‚€ â‰¤ Îµ) âˆ§ (Pe â€¦
  rcases perf1.small_diam_aux Îµ_pos hxâ‚ with âŸ¨perf1', non1', sub1', diam1âŸ©
  -- âŠ¢ âˆƒ Câ‚€ Câ‚, (Perfect Câ‚€ âˆ§ Set.Nonempty Câ‚€ âˆ§ Câ‚€ âŠ† C âˆ§ EMetric.diam Câ‚€ â‰¤ Îµ) âˆ§ (Pe â€¦
  refine'
    âŸ¨closure (EMetric.ball xâ‚€ (Îµ / 2) âˆ© Dâ‚€), closure (EMetric.ball xâ‚ (Îµ / 2) âˆ© Dâ‚),
      âŸ¨perf0', non0', sub0'.trans sub0, diam0âŸ©, âŸ¨perf1', non1', sub1'.trans sub1, diam1âŸ©, _âŸ©
  apply Disjoint.mono _ _ hdisj <;> assumption
  -- âŠ¢ closure (EMetric.ball xâ‚€ (Îµ / 2) âˆ© Dâ‚€) â‰¤ Dâ‚€
                                    -- ğŸ‰ no goals
                                    -- ğŸ‰ no goals
#align perfect.small_diam_splitting Perfect.small_diam_splitting

open CantorScheme

/-- Any nonempty perfect set in a complete metric space admits a continuous injection
from the Cantor space, `â„• â†’ Bool`. -/
theorem Perfect.exists_nat_bool_injection [CompleteSpace Î±] :
    âˆƒ f : (â„• â†’ Bool) â†’ Î±, range f âŠ† C âˆ§ Continuous f âˆ§ Injective f := by
  obtain âŸ¨u, -, upos', huâŸ© := exists_seq_strictAnti_tendsto' (zero_lt_one' â„â‰¥0âˆ)
  -- âŠ¢ âˆƒ f, range f âŠ† C âˆ§ Continuous f âˆ§ Injective f
  have upos := fun n => (upos' n).1
  -- âŠ¢ âˆƒ f, range f âŠ† C âˆ§ Continuous f âˆ§ Injective f
  let P := Subtype fun E : Set Î± => Perfect E âˆ§ E.Nonempty
  -- âŠ¢ âˆƒ f, range f âŠ† C âˆ§ Continuous f âˆ§ Injective f
  choose C0 C1 h0 h1 hdisj using
    fun {C : Set Î±} (hC : Perfect C) (hnonempty : C.Nonempty) {Îµ : â„â‰¥0âˆ} (hÎµ : 0 < Îµ) =>
    hC.small_diam_splitting hnonempty hÎµ
  let DP : List Bool â†’ P := fun l => by
    induction' l with a l ih; Â· exact âŸ¨C, âŸ¨hC, hnonemptyâŸ©âŸ©
    cases a
    Â· use C0 ih.property.1 ih.property.2 (upos l.length.succ)
      exact âŸ¨(h0 _ _ _).1, (h0 _ _ _).2.1âŸ©
    use C1 ih.property.1 ih.property.2 (upos l.length.succ)
    exact âŸ¨(h1 _ _ _).1, (h1 _ _ _).2.1âŸ©
  let D : List Bool â†’ Set Î± := fun l => (DP l).val
  -- âŠ¢ âˆƒ f, range f âŠ† C âˆ§ Continuous f âˆ§ Injective f
  have hanti : ClosureAntitone D := by
    refine' Antitone.closureAntitone _ fun l => (DP l).property.1.closed
    intro l a
    cases a
    Â· exact (h0 _ _ _).2.2.1
    exact (h1 _ _ _).2.2.1
  have hdiam : VanishingDiam D := by
    intro x
    apply tendsto_of_tendsto_of_tendsto_of_le_of_le' tendsto_const_nhds hu
    Â· simp
    rw [eventually_atTop]
    refine' âŸ¨1, fun m (hm : 1 â‰¤ m) => _âŸ©
    rw [Nat.one_le_iff_ne_zero] at hm
    rcases Nat.exists_eq_succ_of_ne_zero hm with âŸ¨n, rflâŸ©
    dsimp
    cases x n
    Â· convert(h0 _ _ _).2.2.2
      rw [PiNat.res_length]
    convert(h1 _ _ _).2.2.2
    rw [PiNat.res_length]
  have hdisj' : CantorScheme.Disjoint D := by
    rintro l (a | a) (b | b) hab <;> try contradiction
    Â· exact hdisj _ _ _
    exact (hdisj _ _ _).symm
  have hdom : âˆ€ {x : â„• â†’ Bool}, x âˆˆ (inducedMap D).1 := fun {x} => by
    rw [hanti.map_of_vanishingDiam hdiam fun l => (DP l).property.2]
    apply mem_univ
  refine' âŸ¨fun x => (inducedMap D).2 âŸ¨x, hdomâŸ©, _, _, _âŸ©
  Â· rintro y âŸ¨x, rflâŸ©
    -- âŠ¢ (fun x => Sigma.snd (inducedMap D) { val := x, property := (_ : x âˆˆ (induced â€¦
    exact map_mem âŸ¨_, hdomâŸ© 0
    -- ğŸ‰ no goals
  Â· apply hdiam.map_continuous.comp
    -- âŠ¢ Continuous fun x => { val := x, property := (_ : x âˆˆ (inducedMap D).fst) }
    continuity
    -- ğŸ‰ no goals
  intro x y hxy
  -- âŠ¢ x = y
  simpa only [â† Subtype.val_inj] using hdisj'.map_injective hxy
  -- ğŸ‰ no goals
#align perfect.exists_nat_bool_injection Perfect.exists_nat_bool_injection

end CantorInjMetric

/-- Any closed uncountable subset of a Polish space admits a continuous injection
from the Cantor space `â„• â†’ Bool`.-/
theorem IsClosed.exists_nat_bool_injection_of_not_countable {Î± : Type*} [TopologicalSpace Î±]
    [PolishSpace Î±] {C : Set Î±} (hC : IsClosed C) (hunc : Â¬C.Countable) :
    âˆƒ f : (â„• â†’ Bool) â†’ Î±, range f âŠ† C âˆ§ Continuous f âˆ§ Function.Injective f := by
  letI := upgradePolishSpace Î±
  -- âŠ¢ âˆƒ f, range f âŠ† C âˆ§ Continuous f âˆ§ Function.Injective f
  obtain âŸ¨D, hD, Dnonempty, hDCâŸ© := exists_perfect_nonempty_of_isClosed_of_not_countable hC hunc
  -- âŠ¢ âˆƒ f, range f âŠ† C âˆ§ Continuous f âˆ§ Function.Injective f
  obtain âŸ¨f, hfD, hfâŸ© := hD.exists_nat_bool_injection Dnonempty
  -- âŠ¢ âˆƒ f, range f âŠ† C âˆ§ Continuous f âˆ§ Function.Injective f
  exact âŸ¨f, hfD.trans hDC, hfâŸ©
  -- ğŸ‰ no goals
#align is_closed.exists_nat_bool_injection_of_not_countable IsClosed.exists_nat_bool_injection_of_not_countable
