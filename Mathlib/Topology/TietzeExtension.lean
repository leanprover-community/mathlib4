/-
Copyright (c) 2021 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import Mathlib.Analysis.SpecificLimits.Basic
import Mathlib.Order.Interval.Set.IsoIoo
import Mathlib.Topology.Order.MonotoneContinuity
import Mathlib.Topology.UrysohnsBounded

#align_import topology.tietze_extension from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Tietze extension theorem

In this file we prove a few version of the Tietze extension theorem. The theorem says that a
continuous function `s ‚Üí ‚Ñù` defined on a closed set in a normal topological space `Y` can be
extended to a continuous function on the whole space. Moreover, if all values of the original
function belong to some (finite or infinite, open or closed) interval, then the extension can be
chosen so that it takes values in the same interval. In particular, if the original function is a
bounded function, then there exists a bounded extension of the same norm.

The proof mostly follows <https://ncatlab.org/nlab/show/Tietze+extension+theorem>. We patch a small
gap in the proof for unbounded functions, see
`exists_extension_forall_exists_le_ge_of_closedEmbedding`.

In addition we provide a class `TietzeExtension` encoding the idea that a topological space
satisfies the Tietze extension theorem. This allows us to get a version of the Tietze extension
theorem that simultaneously applies to `‚Ñù`, `‚Ñù √ó ‚Ñù`, `‚ÑÇ`, `Œπ ‚Üí ‚Ñù`, `‚Ñù‚â•0` et cetera. At some point
in the future, it may be desirable to provide instead a more general approach via
*absolute retracts*, but the current implementation covers the most common use cases easily.

## Implementation notes

We first prove the theorems for a closed embedding `e : X ‚Üí Y` of a topological space into a normal
topological space, then specialize them to the case `X = s : Set Y`, `e = (‚Üë)`.

## Tags

Tietze extension theorem, Urysohn's lemma, normal topological space
-/

/-!  ### The `TietzeExtension` class -/

section TietzeExtensionClass

universe u u‚ÇÅ u‚ÇÇ v w

-- TODO: define *absolute retracts* and then prove they satisfy Tietze extension.
-- Then make instances of that instead and remove this class.
/-- A class encoding the concept that a space satisfies the Tietze extension property. -/
class TietzeExtension (Y : Type v) [TopologicalSpace Y] : Prop where
  exists_restrict_eq' {X : Type u} [TopologicalSpace X] [NormalSpace X] (s : Set X)
    (hs : IsClosed s) (f : C(s, Y)) : ‚àÉ (g : C(X, Y)), g.restrict s = f

variable {X‚ÇÅ : Type u‚ÇÅ} [TopologicalSpace X‚ÇÅ]
variable {X : Type u} [TopologicalSpace X] [NormalSpace X] {s : Set X} (hs : IsClosed s)
variable {e : X‚ÇÅ ‚Üí X} (he : ClosedEmbedding e)
variable {Y : Type v} [TopologicalSpace Y] [TietzeExtension.{u, v} Y]

/-- **Tietze extension theorem** for `TietzeExtension` spaces, a version for a closed set. Let
`s` be a closed set in a normal topological space `X`. Let `f` be a continuous function
on `s` with values in a `TietzeExtension` space `Y`. Then there exists a continuous function
`g : C(X, Y)` such that `g.restrict s = f`. -/
theorem ContinuousMap.exists_restrict_eq (f : C(s, Y)) : ‚àÉ (g : C(X, Y)), g.restrict s = f :=
  TietzeExtension.exists_restrict_eq' s hs f
#align continuous_map.exists_restrict_eq_of_closed ContinuousMap.exists_restrict_eq

/-- **Tietze extension theorem** for `TietzeExtension` spaces. Let `e` be a closed embedding of a
nonempty topological space `X‚ÇÅ` into a normal topological space `X`. Let `f` be a continuous
function on `X‚ÇÅ` with values in a `TietzeExtension` space `Y`. Then there exists a
continuous function `g : C(X, Y)` such that `g ‚àò e = f`. -/
theorem ContinuousMap.exists_extension (f : C(X‚ÇÅ, Y)) :
    ‚àÉ (g : C(X, Y)), g.comp ‚ü®e, he.continuous‚ü© = f := by
  let e' : X‚ÇÅ ‚âÉ‚Çú Set.range e := Homeomorph.ofEmbedding _ he.toEmbedding
  obtain ‚ü®g, hg‚ü© := (f.comp e'.symm).exists_restrict_eq he.isClosed_range
  exact ‚ü®g, by ext x; simpa using congr($(hg) ‚ü®e' x, x, rfl‚ü©)‚ü©

/-- **Tietze extension theorem** for `TietzeExtension` spaces. Let `e` be a closed embedding of a
nonempty topological space `X‚ÇÅ` into a normal topological space `X`. Let `f` be a continuous
function on `X‚ÇÅ` with values in a `TietzeExtension` space `Y`. Then there exists a
continuous function `g : C(X, Y)` such that `g ‚àò e = f`.

This version is provided for convenience and backwards compatibility. Here the composition is
phrased in terms of bare functions. -/
theorem ContinuousMap.exists_extension' (f : C(X‚ÇÅ, Y)) : ‚àÉ (g : C(X, Y)), g ‚àò e = f :=
  f.exists_extension he |>.imp fun g hg ‚Ü¶ by ext x; congrm($(hg) x)
#align continuous_map.exists_extension_of_closed_embedding ContinuousMap.exists_extension'

/-- This theorem is not intended to be used directly because it is rare for a set alone to
satisfy `[TietzeExtension t]`. For example, `Metric.ball` in `‚Ñù` only satisfies it when
the radius is strictly positive, so finding this as an instance will fail.

Instead, it is intended to be used as a constructor for theorems about sets which *do* satisfy
`[TietzeExtension t]` under some hypotheses. -/
theorem ContinuousMap.exists_forall_mem_restrict_eq {Y : Type v} [TopologicalSpace Y] (f : C(s, Y))
    {t : Set Y} (hf : ‚àÄ x, f x ‚àà t) [ht : TietzeExtension.{u, v} t] :
    ‚àÉ (g : C(X, Y)), (‚àÄ x, g x ‚àà t) ‚àß g.restrict s = f := by
  obtain ‚ü®g, hg‚ü© := mk _ (map_continuous f |>.codRestrict hf) |>.exists_restrict_eq hs
  exact ‚ü®comp ‚ü®Subtype.val, by continuity‚ü© g, by simp, by ext x; congrm(($(hg) x : Y))‚ü©

/-- This theorem is not intended to be used directly because it is rare for a set alone to
satisfy `[TietzeExtension t]`. For example, `Metric.ball` in `‚Ñù` only satisfies it when
the radius is strictly positive, so finding this as an instance will fail.

Instead, it is intended to be used as a constructor for theorems about sets which *do* satisfy
`[TietzeExtension t]` under some hypotheses. -/
theorem ContinuousMap.exists_extension_forall_mem {Y : Type v} [TopologicalSpace Y] (f : C(X‚ÇÅ, Y))
    {t : Set Y} (hf : ‚àÄ x, f x ‚àà t) [ht : TietzeExtension.{u, v} t] :
    ‚àÉ (g : C(X, Y)), (‚àÄ x, g x ‚àà t) ‚àß g.comp ‚ü®e, he.continuous‚ü© = f := by
  obtain ‚ü®g, hg‚ü© := mk _ (map_continuous f |>.codRestrict hf) |>.exists_extension he
  exact ‚ü®comp ‚ü®Subtype.val, by continuity‚ü© g, by simp, by ext x; congrm(($(hg) x : Y))‚ü©

instance Pi.instTietzeExtension {Œπ : Type*} {Y : Œπ ‚Üí Type v} [‚àÄ i, TopologicalSpace (Y i)]
    [‚àÄ i, TietzeExtension (Y i)] : TietzeExtension (‚àÄ i, Y i) where
  exists_restrict_eq' s hs f := by
    obtain ‚ü®g', hg'‚ü© := Classical.skolem.mp <| fun i ‚Ü¶
      ContinuousMap.exists_restrict_eq hs (ContinuousMap.piEquiv _ _ |>.symm f i)
    exact ‚ü®ContinuousMap.piEquiv _ _ g', by ext x i; congrm($(hg' i) x)‚ü©

instance Prod.instTietzeExtension {Y : Type v} {Z : Type w} [TopologicalSpace Y]
    [TietzeExtension.{u, v} Y] [TopologicalSpace Z] [TietzeExtension.{u, w} Z] :
    TietzeExtension (Y √ó Z) where
  exists_restrict_eq' s hs f := by
    obtain ‚ü®g‚ÇÅ, hg‚ÇÅ‚ü© := (ContinuousMap.fst.comp f).exists_restrict_eq hs
    obtain ‚ü®g‚ÇÇ, hg‚ÇÇ‚ü© := (ContinuousMap.snd.comp f).exists_restrict_eq hs
    exact ‚ü®g‚ÇÅ.prodMk g‚ÇÇ, by ext1 x; congrm(($(hg‚ÇÅ) x), $(hg‚ÇÇ) x)‚ü©

instance Unique.instTietzeExtension {Y : Type v} [TopologicalSpace Y] [Unique Y] :
    TietzeExtension.{u, v} Y where
  exists_restrict_eq' _ _ f := ‚ü®.const _ default, by ext x; exact Subsingleton.elim _ _‚ü©

/-- Any retract of a `TietzeExtension` space is one itself. -/
theorem TietzeExtension.of_retract {Y : Type v} {Z : Type w} [TopologicalSpace Y]
    [TopologicalSpace Z] [TietzeExtension.{u, w} Z] (Œπ : C(Y, Z)) (r : C(Z, Y))
    (h : r.comp Œπ = .id Y) : TietzeExtension.{u, v} Y where
  exists_restrict_eq' s hs f := by
    obtain ‚ü®g, hg‚ü© := (Œπ.comp f).exists_restrict_eq hs
    use r.comp g
    ext1 x
    have := congr(r.comp $(hg))
    rw [‚Üê r.comp_assoc Œπ, h, f.id_comp] at this
    congrm($this x)

/-- Any homeomorphism from a `TietzeExtension` space is one itself. -/
theorem TietzeExtension.of_homeo {Y : Type v} {Z : Type w} [TopologicalSpace Y]
    [TopologicalSpace Z] [TietzeExtension.{u, w} Z] (e : Y ‚âÉ‚Çú Z) :
    TietzeExtension.{u, v} Y :=
  .of_retract (e : C(Y, Z)) (e.symm : C(Z, Y)) <| by simp

end TietzeExtensionClass

/-! The Tietze extension theorem for `‚Ñù`. -/

variable {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] [NormalSpace Y]

open Metric Set Filter

open BoundedContinuousFunction Topology

noncomputable section

namespace BoundedContinuousFunction

/-- One step in the proof of the Tietze extension theorem. If `e : C(X, Y)` is a closed embedding
of a topological space into a normal topological space and `f : X ‚Üí·µá ‚Ñù` is a bounded continuous
function, then there exists a bounded continuous function `g : Y ‚Üí·µá ‚Ñù` of the norm `‚Äñg‚Äñ ‚â§ ‚Äñf‚Äñ / 3`
such that the distance between `g ‚àò e` and `f` is at most `(2 / 3) * ‚Äñf‚Äñ`. -/
theorem tietze_extension_step (f : X ‚Üí·µá ‚Ñù) (e : C(X, Y)) (he : ClosedEmbedding e) :
    ‚àÉ g : Y ‚Üí·µá ‚Ñù, ‚Äñg‚Äñ ‚â§ ‚Äñf‚Äñ / 3 ‚àß dist (g.compContinuous e) f ‚â§ 2 / 3 * ‚Äñf‚Äñ := by
  have h3 : (0 : ‚Ñù) < 3 := by norm_num1
  have h23 : 0 < (2 / 3 : ‚Ñù) := by norm_num1
  -- In the trivial case `f = 0`, we take `g = 0`
  rcases eq_or_ne f 0 with (rfl | hf)
  ¬∑ use 0
    simp
  replace hf : 0 < ‚Äñf‚Äñ := norm_pos_iff.2 hf
  /- Otherwise, the closed sets `e '' (f ‚Åª¬π' (Iic (-‚Äñf‚Äñ / 3)))` and `e '' (f ‚Åª¬π' (Ici (‚Äñf‚Äñ / 3)))`
    are disjoint, hence by Urysohn's lemma there exists a function `g` that is equal to `-‚Äñf‚Äñ / 3`
    on the former set and is equal to `‚Äñf‚Äñ / 3` on the latter set. This function `g` satisfies the
    assertions of the lemma. -/
  have hf3 : -‚Äñf‚Äñ / 3 < ‚Äñf‚Äñ / 3 := (div_lt_div_right h3).2 (Left.neg_lt_self hf)
  have hc‚ÇÅ : IsClosed (e '' (f ‚Åª¬π' Iic (-‚Äñf‚Äñ / 3))) :=
    he.isClosedMap _ (isClosed_Iic.preimage f.continuous)
  have hc‚ÇÇ : IsClosed (e '' (f ‚Åª¬π' Ici (‚Äñf‚Äñ / 3))) :=
    he.isClosedMap _ (isClosed_Ici.preimage f.continuous)
  have hd : Disjoint (e '' (f ‚Åª¬π' Iic (-‚Äñf‚Äñ / 3))) (e '' (f ‚Åª¬π' Ici (‚Äñf‚Äñ / 3))) := by
    refine disjoint_image_of_injective he.inj (Disjoint.preimage _ ?_)
    rwa [Iic_disjoint_Ici, not_le]
  rcases exists_bounded_mem_Icc_of_closed_of_le hc‚ÇÅ hc‚ÇÇ hd hf3.le with ‚ü®g, hg‚ÇÅ, hg‚ÇÇ, hgf‚ü©
  refine ‚ü®g, ?_, ?_‚ü©
  ¬∑ refine (norm_le <| div_nonneg hf.le h3.le).mpr fun y => ?_
    simpa [abs_le, neg_div] using hgf y
  ¬∑ refine (dist_le <| mul_nonneg h23.le hf.le).mpr fun x => ?_
    have hfx : -‚Äñf‚Äñ ‚â§ f x ‚àß f x ‚â§ ‚Äñf‚Äñ := by
      simpa only [Real.norm_eq_abs, abs_le] using f.norm_coe_le_norm x
    rcases le_total (f x) (-‚Äñf‚Äñ / 3) with hle‚ÇÅ | hle‚ÇÅ
    ¬∑ calc
        |g (e x) - f x| = -‚Äñf‚Äñ / 3 - f x := by
          rw [hg‚ÇÅ (mem_image_of_mem _ hle‚ÇÅ), Function.const_apply,
            abs_of_nonneg (sub_nonneg.2 hle‚ÇÅ)]
        _ ‚â§ 2 / 3 * ‚Äñf‚Äñ := by linarith
    ¬∑ rcases le_total (f x) (‚Äñf‚Äñ / 3) with hle‚ÇÇ | hle‚ÇÇ
      ¬∑ simp only [neg_div] at *
        calc
          dist (g (e x)) (f x) ‚â§ |g (e x)| + |f x| := dist_le_norm_add_norm _ _
          _ ‚â§ ‚Äñf‚Äñ / 3 + ‚Äñf‚Äñ / 3 := (add_le_add (abs_le.2 <| hgf _) (abs_le.2 ‚ü®hle‚ÇÅ, hle‚ÇÇ‚ü©))
          _ = 2 / 3 * ‚Äñf‚Äñ := by linarith
      ¬∑ calc
          |g (e x) - f x| = f x - ‚Äñf‚Äñ / 3 := by
            rw [hg‚ÇÇ (mem_image_of_mem _ hle‚ÇÇ), abs_sub_comm, Function.const_apply,
              abs_of_nonneg (sub_nonneg.2 hle‚ÇÇ)]
          _ ‚â§ 2 / 3 * ‚Äñf‚Äñ := by linarith
#align bounded_continuous_function.tietze_extension_step BoundedContinuousFunction.tietze_extension_step

/-- **Tietze extension theorem** for real-valued bounded continuous maps, a version with a closed
embedding and bundled composition. If `e : C(X, Y)` is a closed embedding of a topological space
into a normal topological space and `f : X ‚Üí·µá ‚Ñù` is a bounded continuous function, then there exists
a bounded continuous function `g : Y ‚Üí·µá ‚Ñù` of the same norm such that `g ‚àò e = f`. -/
theorem exists_extension_norm_eq_of_closedEmbedding' (f : X ‚Üí·µá ‚Ñù) (e : C(X, Y))
    (he : ClosedEmbedding e) : ‚àÉ g : Y ‚Üí·µá ‚Ñù, ‚Äñg‚Äñ = ‚Äñf‚Äñ ‚àß g.compContinuous e = f := by
  /- For the proof, we iterate `tietze_extension_step`. Each time we apply it to the difference
    between the previous approximation and `f`. -/
  choose F hF_norm hF_dist using fun f : X ‚Üí·µá ‚Ñù => tietze_extension_step f e he
  set g : ‚Ñï ‚Üí Y ‚Üí·µá ‚Ñù := fun n => (fun g => g + F (f - g.compContinuous e))^[n] 0
  have g0 : g 0 = 0 := rfl
  have g_succ : ‚àÄ n, g (n + 1) = g n + F (f - (g n).compContinuous e) := fun n =>
    Function.iterate_succ_apply' _ _ _
  have hgf : ‚àÄ n, dist ((g n).compContinuous e) f ‚â§ (2 / 3) ^ n * ‚Äñf‚Äñ := by
    intro n
    induction' n with n ihn
    ¬∑ simp [g0]
    ¬∑ rw [g_succ n, add_compContinuous, ‚Üê dist_sub_right, add_sub_cancel_left, pow_succ', mul_assoc]
      refine (hF_dist _).trans (mul_le_mul_of_nonneg_left ?_ (by norm_num1))
      rwa [‚Üê dist_eq_norm']
  have hg_dist : ‚àÄ n, dist (g n) (g (n + 1)) ‚â§ 1 / 3 * ‚Äñf‚Äñ * (2 / 3) ^ n := by
    intro n
    calc
      dist (g n) (g (n + 1)) = ‚ÄñF (f - (g n).compContinuous e)‚Äñ := by
        rw [g_succ, dist_eq_norm', add_sub_cancel_left]
      _ ‚â§ ‚Äñf - (g n).compContinuous e‚Äñ / 3 := hF_norm _
      _ = 1 / 3 * dist ((g n).compContinuous e) f := by rw [dist_eq_norm', one_div, div_eq_inv_mul]
      _ ‚â§ 1 / 3 * ((2 / 3) ^ n * ‚Äñf‚Äñ) := mul_le_mul_of_nonneg_left (hgf n) (by norm_num1)
      _ = 1 / 3 * ‚Äñf‚Äñ * (2 / 3) ^ n := by ac_rfl
  have hg_cau : CauchySeq g := cauchySeq_of_le_geometric _ _ (by norm_num1) hg_dist
  have :
    Tendsto (fun n => (g n).compContinuous e) atTop
      (ùìù <| (limUnder atTop g).compContinuous e) :=
    ((continuous_compContinuous e).tendsto _).comp hg_cau.tendsto_limUnder
  have hge : (limUnder atTop g).compContinuous e = f := by
    refine tendsto_nhds_unique this (tendsto_iff_dist_tendsto_zero.2 ?_)
    refine squeeze_zero (fun _ => dist_nonneg) hgf ?_
    rw [‚Üê zero_mul ‚Äñf‚Äñ]
    refine (tendsto_pow_atTop_nhds_zero_of_lt_one ?_ ?_).mul tendsto_const_nhds <;> norm_num1
  refine ‚ü®limUnder atTop g, le_antisymm ?_ ?_, hge‚ü©
  ¬∑ rw [‚Üê dist_zero_left, ‚Üê g0]
    refine
      (dist_le_of_le_geometric_of_tendsto‚ÇÄ _ _ (by norm_num1)
        hg_dist hg_cau.tendsto_limUnder).trans_eq ?_
    field_simp [show (3 - 2 : ‚Ñù) = 1 by norm_num1]
  ¬∑ rw [‚Üê hge]
    exact norm_compContinuous_le _ _
#align bounded_continuous_function.exists_extension_norm_eq_of_closed_embedding' BoundedContinuousFunction.exists_extension_norm_eq_of_closedEmbedding'

/-- **Tietze extension theorem** for real-valued bounded continuous maps, a version with a closed
embedding and unbundled composition. If `e : C(X, Y)` is a closed embedding of a topological space
into a normal topological space and `f : X ‚Üí·µá ‚Ñù` is a bounded continuous function, then there exists
a bounded continuous function `g : Y ‚Üí·µá ‚Ñù` of the same norm such that `g ‚àò e = f`. -/
theorem exists_extension_norm_eq_of_closedEmbedding (f : X ‚Üí·µá ‚Ñù) {e : X ‚Üí Y}
    (he : ClosedEmbedding e) : ‚àÉ g : Y ‚Üí·µá ‚Ñù, ‚Äñg‚Äñ = ‚Äñf‚Äñ ‚àß g ‚àò e = f := by
  rcases exists_extension_norm_eq_of_closedEmbedding' f ‚ü®e, he.continuous‚ü© he with ‚ü®g, hg, rfl‚ü©
  exact ‚ü®g, hg, rfl‚ü©
#align bounded_continuous_function.exists_extension_norm_eq_of_closed_embedding BoundedContinuousFunction.exists_extension_norm_eq_of_closedEmbedding

/-- **Tietze extension theorem** for real-valued bounded continuous maps, a version for a closed
set. If `f` is a bounded continuous real-valued function defined on a closed set in a normal
topological space, then it can be extended to a bounded continuous function of the same norm defined
on the whole space. -/
theorem exists_norm_eq_restrict_eq_of_closed {s : Set Y} (f : s ‚Üí·µá ‚Ñù) (hs : IsClosed s) :
    ‚àÉ g : Y ‚Üí·µá ‚Ñù, ‚Äñg‚Äñ = ‚Äñf‚Äñ ‚àß g.restrict s = f :=
  exists_extension_norm_eq_of_closedEmbedding' f ((ContinuousMap.id _).restrict s)
    (closedEmbedding_subtype_val hs)
#align bounded_continuous_function.exists_norm_eq_restrict_eq_of_closed BoundedContinuousFunction.exists_norm_eq_restrict_eq_of_closed

/-- **Tietze extension theorem** for real-valued bounded continuous maps, a version for a closed
embedding and a bounded continuous function that takes values in a non-trivial closed interval.
See also `exists_extension_forall_mem_of_closedEmbedding` for a more general statement that works
for any interval (finite or infinite, open or closed).

If `e : X ‚Üí Y` is a closed embedding and `f : X ‚Üí·µá ‚Ñù` is a bounded continuous function such that
`f x ‚àà [a, b]` for all `x`, where `a ‚â§ b`, then there exists a bounded continuous function
`g : Y ‚Üí·µá ‚Ñù` such that `g y ‚àà [a, b]` for all `y` and `g ‚àò e = f`. -/
theorem exists_extension_forall_mem_Icc_of_closedEmbedding (f : X ‚Üí·µá ‚Ñù) {a b : ‚Ñù} {e : X ‚Üí Y}
    (hf : ‚àÄ x, f x ‚àà Icc a b) (hle : a ‚â§ b) (he : ClosedEmbedding e) :
    ‚àÉ g : Y ‚Üí·µá ‚Ñù, (‚àÄ y, g y ‚àà Icc a b) ‚àß g ‚àò e = f := by
  rcases exists_extension_norm_eq_of_closedEmbedding (f - const X ((a + b) / 2)) he with
    ‚ü®g, hgf, hge‚ü©
  refine ‚ü®const Y ((a + b) / 2) + g, fun y => ?_, ?_‚ü©
  ¬∑ suffices ‚Äñf - const X ((a + b) / 2)‚Äñ ‚â§ (b - a) / 2 by
      simpa [Real.Icc_eq_closedBall, add_mem_closedBall_iff_norm] using
        (norm_coe_le_norm g y).trans (hgf.trans_le this)
    refine (norm_le <| div_nonneg (sub_nonneg.2 hle) zero_le_two).2 fun x => ?_
    simpa only [Real.Icc_eq_closedBall] using hf x
  ¬∑ ext x
    have : g (e x) = f x - (a + b) / 2 := congr_fun hge x
    simp [this]
#align bounded_continuous_function.exists_extension_forall_mem_Icc_of_closed_embedding BoundedContinuousFunction.exists_extension_forall_mem_Icc_of_closedEmbedding

/-- **Tietze extension theorem** for real-valued bounded continuous maps, a version for a closed
embedding. Let `e` be a closed embedding of a nonempty topological space `X` into a normal
topological space `Y`. Let `f` be a bounded continuous real-valued function on `X`. Then there
exists a bounded continuous function `g : Y ‚Üí·µá ‚Ñù` such that `g ‚àò e = f` and each value `g y` belongs
to a closed interval `[f x‚ÇÅ, f x‚ÇÇ]` for some `x‚ÇÅ` and `x‚ÇÇ`.  -/
theorem exists_extension_forall_exists_le_ge_of_closedEmbedding [Nonempty X] (f : X ‚Üí·µá ‚Ñù)
    {e : X ‚Üí Y} (he : ClosedEmbedding e) :
    ‚àÉ g : Y ‚Üí·µá ‚Ñù, (‚àÄ y, ‚àÉ x‚ÇÅ x‚ÇÇ, g y ‚àà Icc (f x‚ÇÅ) (f x‚ÇÇ)) ‚àß g ‚àò e = f := by
  inhabit X
  -- Put `a = ‚®Ö x, f x` and `b = ‚®Ü x, f x`
  obtain ‚ü®a, ha‚ü© : ‚àÉ a, IsGLB (range f) a := ‚ü®_, isGLB_ciInf f.isBounded_range.bddBelow‚ü©
  obtain ‚ü®b, hb‚ü© : ‚àÉ b, IsLUB (range f) b := ‚ü®_, isLUB_ciSup f.isBounded_range.bddAbove‚ü©
  -- Then `f x ‚àà [a, b]` for all `x`
  have hmem : ‚àÄ x, f x ‚àà Icc a b := fun x => ‚ü®ha.1 ‚ü®x, rfl‚ü©, hb.1 ‚ü®x, rfl‚ü©‚ü©
  -- Rule out the trivial case `a = b`
  have hle : a ‚â§ b := (hmem default).1.trans (hmem default).2
  rcases hle.eq_or_lt with (rfl | hlt)
  ¬∑ have : ‚àÄ x, f x = a := by simpa using hmem
    use const Y a
    simp [this, Function.funext_iff]
  -- Put `c = (a + b) / 2`. Then `a < c < b` and `c - a = b - c`.
  set c := (a + b) / 2
  have hac : a < c := left_lt_add_div_two.2 hlt
  have hcb : c < b := add_div_two_lt_right.2 hlt
  have hsub : c - a = b - c := by
    field_simp [c]
    ring
  /- Due to `exists_extension_forall_mem_Icc_of_closedEmbedding`, there exists an extension `g`
    such that `g y ‚àà [a, b]` for all `y`. However, if `a` and/or `b` do not belong to the range of
    `f`, then we need to ensure that these points do not belong to the range of `g`. This is done
    in two almost identical steps. First we deal with the case `‚àÄ x, f x ‚â† a`. -/
  obtain ‚ü®g, hg_mem, hgf‚ü© : ‚àÉ g : Y ‚Üí·µá ‚Ñù, (‚àÄ y, ‚àÉ x, g y ‚àà Icc (f x) b) ‚àß g ‚àò e = f := by
    rcases exists_extension_forall_mem_Icc_of_closedEmbedding f hmem hle he with ‚ü®g, hg_mem, hgf‚ü©
    -- If `a ‚àà range f`, then we are done.
    rcases em (‚àÉ x, f x = a) with (‚ü®x, rfl‚ü© | ha')
    ¬∑ exact ‚ü®g, fun y => ‚ü®x, hg_mem _‚ü©, hgf‚ü©
    /- Otherwise, `g ‚Åª¬π' {a}` is disjoint with `range e ‚à™ g ‚Åª¬π' (Ici c)`, hence there exists a
        function `dg : Y ‚Üí ‚Ñù` such that `dg ‚àò e = 0`, `dg y = 0` whenever `c ‚â§ g y`, `dg y = c - a`
        whenever `g y = a`, and `0 ‚â§ dg y ‚â§ c - a` for all `y`.  -/
    have hd : Disjoint (range e ‚à™ g ‚Åª¬π' Ici c) (g ‚Åª¬π' {a}) := by
      refine disjoint_union_left.2 ‚ü®?_, Disjoint.preimage _ ?_‚ü©
      ¬∑ rw [Set.disjoint_left]
        rintro _ ‚ü®x, rfl‚ü© (rfl : g (e x) = a)
        exact ha' ‚ü®x, (congr_fun hgf x).symm‚ü©
      ¬∑ exact Set.disjoint_singleton_right.2 hac.not_le
    rcases exists_bounded_mem_Icc_of_closed_of_le
        (he.isClosed_range.union <| isClosed_Ici.preimage g.continuous)
        (isClosed_singleton.preimage g.continuous) hd (sub_nonneg.2 hac.le) with
      ‚ü®dg, dg0, dga, dgmem‚ü©
    replace hgf : ‚àÄ x, (g + dg) (e x) = f x := by
      intro x
      simp [dg0 (Or.inl <| mem_range_self _), ‚Üê hgf]
    refine ‚ü®g + dg, fun y => ?_, funext hgf‚ü©
    have hay : a < (g + dg) y := by
      rcases (hg_mem y).1.eq_or_lt with (rfl | hlt)
      ¬∑ refine (lt_add_iff_pos_right _).2 ?_
        calc
          0 < c - g y := sub_pos.2 hac
          _ = dg y := (dga rfl).symm
      ¬∑ exact hlt.trans_le (le_add_of_nonneg_right (dgmem y).1)
    rcases ha.exists_between hay with ‚ü®_, ‚ü®x, rfl‚ü©, _, hxy‚ü©
    refine ‚ü®x, hxy.le, ?_‚ü©
    rcases le_total c (g y) with hc | hc
    ¬∑ simp [dg0 (Or.inr hc), (hg_mem y).2]
    ¬∑ calc
        g y + dg y ‚â§ c + (c - a) := add_le_add hc (dgmem _).2
        _ = b := by rw [hsub, add_sub_cancel]
  /- Now we deal with the case `‚àÄ x, f x ‚â† b`. The proof is the same as in the first case, with
    minor modifications that make it hard to deduplicate code. -/
  choose xl hxl hgb using hg_mem
  rcases em (‚àÉ x, f x = b) with (‚ü®x, rfl‚ü© | hb')
  ¬∑ exact ‚ü®g, fun y => ‚ü®xl y, x, hxl y, hgb y‚ü©, hgf‚ü©
  have hd : Disjoint (range e ‚à™ g ‚Åª¬π' Iic c) (g ‚Åª¬π' {b}) := by
    refine disjoint_union_left.2 ‚ü®?_, Disjoint.preimage _ ?_‚ü©
    ¬∑ rw [Set.disjoint_left]
      rintro _ ‚ü®x, rfl‚ü© (rfl : g (e x) = b)
      exact hb' ‚ü®x, (congr_fun hgf x).symm‚ü©
    ¬∑ exact Set.disjoint_singleton_right.2 hcb.not_le
  rcases exists_bounded_mem_Icc_of_closed_of_le
      (he.isClosed_range.union <| isClosed_Iic.preimage g.continuous)
      (isClosed_singleton.preimage g.continuous) hd (sub_nonneg.2 hcb.le) with
    ‚ü®dg, dg0, dgb, dgmem‚ü©
  replace hgf : ‚àÄ x, (g - dg) (e x) = f x := by
    intro x
    simp [dg0 (Or.inl <| mem_range_self _), ‚Üê hgf]
  refine ‚ü®g - dg, fun y => ?_, funext hgf‚ü©
  have hyb : (g - dg) y < b := by
    rcases (hgb y).eq_or_lt with (rfl | hlt)
    ¬∑ refine (sub_lt_self_iff _).2 ?_
      calc
        0 < g y - c := sub_pos.2 hcb
        _ = dg y := (dgb rfl).symm
    ¬∑ exact ((sub_le_self_iff _).2 (dgmem _).1).trans_lt hlt
  rcases hb.exists_between hyb with ‚ü®_, ‚ü®xu, rfl‚ü©, hyxu, _‚ü©
  cases' lt_or_le c (g y) with hc hc
  ¬∑ rcases em (a ‚àà range f) with (‚ü®x, rfl‚ü© | _)
    ¬∑ refine ‚ü®x, xu, ?_, hyxu.le‚ü©
      calc
        f x = c - (b - c) := by rw [‚Üê hsub, sub_sub_cancel]
        _ ‚â§ g y - dg y := sub_le_sub hc.le (dgmem _).2
    ¬∑ have hay : a < (g - dg) y := by
        calc
          a = c - (b - c) := by rw [‚Üê hsub, sub_sub_cancel]
          _ < g y - (b - c) := sub_lt_sub_right hc _
          _ ‚â§ g y - dg y := sub_le_sub_left (dgmem _).2 _
      rcases ha.exists_between hay with ‚ü®_, ‚ü®x, rfl‚ü©, _, hxy‚ü©
      exact ‚ü®x, xu, hxy.le, hyxu.le‚ü©
  ¬∑ refine ‚ü®xl y, xu, ?_, hyxu.le‚ü©
    simp [dg0 (Or.inr hc), hxl]
#align bounded_continuous_function.exists_extension_forall_exists_le_ge_of_closed_embedding BoundedContinuousFunction.exists_extension_forall_exists_le_ge_of_closedEmbedding

/-- **Tietze extension theorem** for real-valued bounded continuous maps, a version for a closed
embedding. Let `e` be a closed embedding of a nonempty topological space `X` into a normal
topological space `Y`. Let `f` be a bounded continuous real-valued function on `X`. Let `t` be
a nonempty convex set of real numbers (we use `OrdConnected` instead of `Convex` to automatically
deduce this argument by typeclass search) such that `f x ‚àà t` for all `x`. Then there exists
a bounded continuous real-valued function `g : Y ‚Üí·µá ‚Ñù` such that `g y ‚àà t` for all `y` and
`g ‚àò e = f`. -/
theorem exists_extension_forall_mem_of_closedEmbedding (f : X ‚Üí·µá ‚Ñù) {t : Set ‚Ñù} {e : X ‚Üí Y}
    [hs : OrdConnected t] (hf : ‚àÄ x, f x ‚àà t) (hne : t.Nonempty) (he : ClosedEmbedding e) :
    ‚àÉ g : Y ‚Üí·µá ‚Ñù, (‚àÄ y, g y ‚àà t) ‚àß g ‚àò e = f := by
  cases isEmpty_or_nonempty X
  ¬∑ rcases hne with ‚ü®c, hc‚ü©
    exact ‚ü®const Y c, fun _ => hc, funext fun x => isEmptyElim x‚ü©
  rcases exists_extension_forall_exists_le_ge_of_closedEmbedding f he with ‚ü®g, hg, hgf‚ü©
  refine ‚ü®g, fun y => ?_, hgf‚ü©
  rcases hg y with ‚ü®xl, xu, h‚ü©
  exact hs.out (hf _) (hf _) h
#align bounded_continuous_function.exists_extension_forall_mem_of_closed_embedding BoundedContinuousFunction.exists_extension_forall_mem_of_closedEmbedding

/-- **Tietze extension theorem** for real-valued bounded continuous maps, a version for a closed
set. Let `s` be a closed set in a normal topological space `Y`. Let `f` be a bounded continuous
real-valued function on `s`. Let `t` be a nonempty convex set of real numbers (we use
`OrdConnected` instead of `Convex` to automatically deduce this argument by typeclass search) such
that `f x ‚àà t` for all `x : s`. Then there exists a bounded continuous real-valued function
`g : Y ‚Üí·µá ‚Ñù` such that `g y ‚àà t` for all `y` and `g.restrict s = f`. -/
theorem exists_forall_mem_restrict_eq_of_closed {s : Set Y} (f : s ‚Üí·µá ‚Ñù) (hs : IsClosed s)
    {t : Set ‚Ñù} [OrdConnected t] (hf : ‚àÄ x, f x ‚àà t) (hne : t.Nonempty) :
    ‚àÉ g : Y ‚Üí·µá ‚Ñù, (‚àÄ y, g y ‚àà t) ‚àß g.restrict s = f := by
  rcases exists_extension_forall_mem_of_closedEmbedding f hf hne
      (closedEmbedding_subtype_val hs) with
    ‚ü®g, hg, hgf‚ü©
  exact ‚ü®g, hg, DFunLike.coe_injective hgf‚ü©
#align bounded_continuous_function.exists_forall_mem_restrict_eq_of_closed BoundedContinuousFunction.exists_forall_mem_restrict_eq_of_closed

end BoundedContinuousFunction

namespace ContinuousMap

/-- **Tietze extension theorem** for real-valued continuous maps, a version for a closed
embedding. Let `e` be a closed embedding of a nonempty topological space `X` into a normal
topological space `Y`. Let `f` be a continuous real-valued function on `X`. Let `t` be a nonempty
convex set of real numbers (we use `OrdConnected` instead of `Convex` to automatically deduce this
argument by typeclass search) such that `f x ‚àà t` for all `x`. Then there exists a continuous
real-valued function `g : C(Y, ‚Ñù)` such that `g y ‚àà t` for all `y` and `g ‚àò e = f`. -/
theorem exists_extension_forall_mem_of_closedEmbedding (f : C(X, ‚Ñù)) {t : Set ‚Ñù} {e : X ‚Üí Y}
    [hs : OrdConnected t] (hf : ‚àÄ x, f x ‚àà t) (hne : t.Nonempty) (he : ClosedEmbedding e) :
    ‚àÉ g : C(Y, ‚Ñù), (‚àÄ y, g y ‚àà t) ‚àß g ‚àò e = f := by
  have h : ‚Ñù ‚âÉo Ioo (-1 : ‚Ñù) 1 := orderIsoIooNegOneOne ‚Ñù
  let F : X ‚Üí·µá ‚Ñù :=
    { toFun := (‚Üë) ‚àò h ‚àò f
      continuous_toFun := continuous_subtype_val.comp (h.continuous.comp f.continuous)
      map_bounded' := isBounded_range_iff.1
        ((isBounded_Ioo (-1 : ‚Ñù) 1).subset <| range_subset_iff.2 fun x => (h (f x)).2) }
  let t' : Set ‚Ñù := (‚Üë) ‚àò h '' t
  have ht_sub : t' ‚äÜ Ioo (-1 : ‚Ñù) 1 := image_subset_iff.2 fun x _ => (h x).2
  have : OrdConnected t' := by
    constructor
    rintro _ ‚ü®x, hx, rfl‚ü© _ ‚ü®y, hy, rfl‚ü© z hz
    lift z to Ioo (-1 : ‚Ñù) 1 using Icc_subset_Ioo (h x).2.1 (h y).2.2 hz
    change z ‚àà Icc (h x) (h y) at hz
    rw [‚Üê h.image_Icc] at hz
    rcases hz with ‚ü®z, hz, rfl‚ü©
    exact ‚ü®z, hs.out hx hy hz, rfl‚ü©
  have hFt : ‚àÄ x, F x ‚àà t' := fun x => mem_image_of_mem _ (hf x)
  rcases F.exists_extension_forall_mem_of_closedEmbedding hFt (hne.image _) he with ‚ü®G, hG, hGF‚ü©
  let g : C(Y, ‚Ñù) :=
    ‚ü®h.symm ‚àò codRestrict G _ fun y => ht_sub (hG y),
      h.symm.continuous.comp <| G.continuous.subtype_mk _‚ü©
  have hgG : ‚àÄ {y a}, g y = a ‚Üî G y = h a := @fun y a =>
    h.toEquiv.symm_apply_eq.trans Subtype.ext_iff
  refine ‚ü®g, fun y => ?_, ?_‚ü©
  ¬∑ rcases hG y with ‚ü®a, ha, hay‚ü©
    convert ha
    exact hgG.2 hay.symm
  ¬∑ ext x
    exact hgG.2 (congr_fun hGF _)
#align continuous_map.exists_extension_forall_mem_of_closed_embedding ContinuousMap.exists_extension_forall_mem_of_closedEmbedding

@[deprecated (since := "2024-01-16")]
alias exists_extension_of_closedEmbedding := exists_extension'

/-- **Tietze extension theorem** for real-valued continuous maps, a version for a closed set. Let
`s` be a closed set in a normal topological space `Y`. Let `f` be a continuous real-valued function
on `s`. Let `t` be a nonempty convex set of real numbers (we use `OrdConnected` instead of `Convex`
to automatically deduce this argument by typeclass search) such that `f x ‚àà t` for all `x : s`. Then
there exists a continuous real-valued function `g : C(Y, ‚Ñù)` such that `g y ‚àà t` for all `y` and
`g.restrict s = f`. -/
theorem exists_restrict_eq_forall_mem_of_closed {s : Set Y} (f : C(s, ‚Ñù)) {t : Set ‚Ñù}
    [OrdConnected t] (ht : ‚àÄ x, f x ‚àà t) (hne : t.Nonempty) (hs : IsClosed s) :
    ‚àÉ g : C(Y, ‚Ñù), (‚àÄ y, g y ‚àà t) ‚àß g.restrict s = f :=
  let ‚ü®g, hgt, hgf‚ü© :=
    exists_extension_forall_mem_of_closedEmbedding f ht hne (closedEmbedding_subtype_val hs)
  ‚ü®g, hgt, coe_injective hgf‚ü©
#align continuous_map.exists_restrict_eq_forall_mem_of_closed ContinuousMap.exists_restrict_eq_forall_mem_of_closed

@[deprecated (since := "2024-01-16")] alias exists_restrict_eq_of_closed := exists_restrict_eq

end ContinuousMap

/-- **Tietze extension theorem** for real-valued continuous maps.
`‚Ñù` is a `TietzeExtension` space. -/
instance Real.instTietzeExtension : TietzeExtension ‚Ñù where
  exists_restrict_eq' _s hs f :=
    f.exists_restrict_eq_forall_mem_of_closed (fun _ => mem_univ _) univ_nonempty hs |>.imp
      fun _ ‚Ü¶ (And.right ¬∑)

open NNReal in
/-- **Tietze extension theorem** for nonnegative real-valued continuous maps.
`‚Ñù‚â•0` is a `TietzeExtension` space. -/
instance NNReal.instTietzeExtension : TietzeExtension ‚Ñù‚â•0 :=
  .of_retract ‚ü®((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù), by continuity‚ü© ‚ü®Real.toNNReal, continuous_real_toNNReal‚ü© <| by
    ext; simp
