/-
Copyright (c) 2021 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import Mathlib.Analysis.SpecificLimits.Basic
import Mathlib.Data.Set.Intervals.IsoIoo
import Mathlib.Topology.Algebra.Order.MonotoneContinuity
import Mathlib.Topology.UrysohnsBounded

#align_import topology.tietze_extension from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Tietze extension theorem

In this file we prove a few version of the Tietze extension theorem. The theorem says that a
continuous function `s â†’ â„` defined on a closed set in a normal topological space `Y` can be
extended to a continuous function on the whole space. Moreover, if all values of the original
function belong to some (finite or infinite, open or closed) interval, then the extension can be
chosen so that it takes values in the same interval. In particular, if the original function is a
bounded function, then there exists a bounded extension of the same norm.

The proof mostly follows <https://ncatlab.org/nlab/show/Tietze+extension+theorem>. We patch a small
gap in the proof for unbounded functions, see
`exists_extension_forall_exists_le_ge_of_closedEmbedding`.

## Implementation notes

We first prove the theorems for a closed embedding `e : X â†’ Y` of a topological space into a normal
topological space, then specialize them to the case `X = s : Set Y`, `e = (â†‘)`.

## Tags

Tietze extension theorem, Urysohn's lemma, normal topological space
-/


variable {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] [NormalSpace Y]

open Metric Set Filter

open BoundedContinuousFunction Topology

noncomputable section

namespace BoundedContinuousFunction

/-- One step in the proof of the Tietze extension theorem. If `e : C(X, Y)` is a closed embedding
of a topological space into a normal topological space and `f : X â†’áµ‡ â„` is a bounded continuous
function, then there exists a bounded continuous function `g : Y â†’áµ‡ â„` of the norm `â€–gâ€– â‰¤ â€–fâ€– / 3`
such that the distance between `g âˆ˜ e` and `f` is at most `(2 / 3) * â€–fâ€–`. -/
theorem tietze_extension_step (f : X â†’áµ‡ â„) (e : C(X, Y)) (he : ClosedEmbedding e) :
    âˆƒ g : Y â†’áµ‡ â„, â€–gâ€– â‰¤ â€–fâ€– / 3 âˆ§ dist (g.compContinuous e) f â‰¤ 2 / 3 * â€–fâ€– := by
  have h3 : (0 : â„) < 3 := by norm_num1
  -- âŠ¢ âˆƒ g, â€–gâ€– â‰¤ â€–fâ€– / 3 âˆ§ dist (compContinuous g e) f â‰¤ 2 / 3 * â€–fâ€–
  have h23 : 0 < (2 / 3 : â„) := by norm_num1
  -- âŠ¢ âˆƒ g, â€–gâ€– â‰¤ â€–fâ€– / 3 âˆ§ dist (compContinuous g e) f â‰¤ 2 / 3 * â€–fâ€–
  -- In the trivial case `f = 0`, we take `g = 0`
  rcases eq_or_ne f 0 with (rfl | hf)
  -- âŠ¢ âˆƒ g, â€–gâ€– â‰¤ â€–0â€– / 3 âˆ§ dist (compContinuous g e) 0 â‰¤ 2 / 3 * â€–0â€–
  Â· use 0
    -- âŠ¢ â€–0â€– â‰¤ â€–0â€– / 3 âˆ§ dist (compContinuous 0 e) 0 â‰¤ 2 / 3 * â€–0â€–
    simp
    -- ğŸ‰ no goals
  replace hf : 0 < â€–fâ€– := norm_pos_iff.2 hf
  -- âŠ¢ âˆƒ g, â€–gâ€– â‰¤ â€–fâ€– / 3 âˆ§ dist (compContinuous g e) f â‰¤ 2 / 3 * â€–fâ€–
  /- Otherwise, the closed sets `e '' (f â»Â¹' (Iic (-â€–fâ€– / 3)))` and `e '' (f â»Â¹' (Ici (â€–fâ€– / 3)))`
    are disjoint, hence by Urysohn's lemma there exists a function `g` that is equal to `-â€–fâ€– / 3`
    on the former set and is equal to `â€–fâ€– / 3` on the latter set. This function `g` satisfies the
    assertions of the lemma. -/
  have hf3 : -â€–fâ€– / 3 < â€–fâ€– / 3 := (div_lt_div_right h3).2 (Left.neg_lt_self hf)
  -- âŠ¢ âˆƒ g, â€–gâ€– â‰¤ â€–fâ€– / 3 âˆ§ dist (compContinuous g e) f â‰¤ 2 / 3 * â€–fâ€–
  have hcâ‚ : IsClosed (e '' (f â»Â¹' Iic (-â€–fâ€– / 3))) :=
    he.isClosedMap _ (isClosed_Iic.preimage f.continuous)
  have hcâ‚‚ : IsClosed (e '' (f â»Â¹' Ici (â€–fâ€– / 3))) :=
    he.isClosedMap _ (isClosed_Ici.preimage f.continuous)
  have hd : Disjoint (e '' (f â»Â¹' Iic (-â€–fâ€– / 3))) (e '' (f â»Â¹' Ici (â€–fâ€– / 3))) := by
    refine' disjoint_image_of_injective he.inj (Disjoint.preimage _ _)
    rwa [Iic_disjoint_Ici, not_le]
  rcases exists_bounded_mem_Icc_of_closed_of_le hcâ‚ hcâ‚‚ hd hf3.le with âŸ¨g, hgâ‚, hgâ‚‚, hgfâŸ©
  -- âŠ¢ âˆƒ g, â€–gâ€– â‰¤ â€–fâ€– / 3 âˆ§ dist (compContinuous g e) f â‰¤ 2 / 3 * â€–fâ€–
  refine' âŸ¨g, _, _âŸ©
  -- âŠ¢ â€–gâ€– â‰¤ â€–fâ€– / 3
  Â· refine' (norm_le <| div_nonneg hf.le h3.le).mpr fun y => _
    -- âŠ¢ â€–â†‘g yâ€– â‰¤ â€–fâ€– / 3
    simpa [abs_le, neg_div] using hgf y
    -- ğŸ‰ no goals
  Â· refine' (dist_le <| mul_nonneg h23.le hf.le).mpr fun x => _
    -- âŠ¢ dist (â†‘(compContinuous g e) x) (â†‘f x) â‰¤ 2 / 3 * â€–fâ€–
    have hfx : -â€–fâ€– â‰¤ f x âˆ§ f x â‰¤ â€–fâ€– := by
      simpa only [Real.norm_eq_abs, abs_le] using f.norm_coe_le_norm x
    cases' le_total (f x) (-â€–fâ€– / 3) with hleâ‚ hleâ‚
    -- âŠ¢ dist (â†‘(compContinuous g e) x) (â†‘f x) â‰¤ 2 / 3 * â€–fâ€–
    Â· calc
        |g (e x) - f x| = -â€–fâ€– / 3 - f x := by
          rw [hgâ‚ (mem_image_of_mem _ hleâ‚), Function.const_apply,
            abs_of_nonneg (sub_nonneg.2 hleâ‚)]
        _ â‰¤ 2 / 3 * â€–fâ€– := by linarith
    Â· cases' le_total (f x) (â€–fâ€– / 3) with hleâ‚‚ hleâ‚‚
      -- âŠ¢ dist (â†‘(compContinuous g e) x) (â†‘f x) â‰¤ 2 / 3 * â€–fâ€–
      Â· simp only [neg_div] at *
        -- âŠ¢ dist (â†‘(compContinuous g e) x) (â†‘f x) â‰¤ 2 / 3 * â€–fâ€–
        calc
          dist (g (e x)) (f x) â‰¤ |g (e x)| + |f x| := dist_le_norm_add_norm _ _
          _ â‰¤ â€–fâ€– / 3 + â€–fâ€– / 3 := (add_le_add (abs_le.2 <| hgf _) (abs_le.2 âŸ¨hleâ‚, hleâ‚‚âŸ©))
          _ = 2 / 3 * â€–fâ€– := by linarith
      Â· calc
          |g (e x) - f x| = f x - â€–fâ€– / 3 := by
            rw [hgâ‚‚ (mem_image_of_mem _ hleâ‚‚), abs_sub_comm, Function.const_apply,
              abs_of_nonneg (sub_nonneg.2 hleâ‚‚)]
          _ â‰¤ 2 / 3 * â€–fâ€– := by linarith
#align bounded_continuous_function.tietze_extension_step BoundedContinuousFunction.tietze_extension_step

/-- **Tietze extension theorem** for real-valued bounded continuous maps, a version with a closed
embedding and bundled composition. If `e : C(X, Y)` is a closed embedding of a topological space
into a normal topological space and `f : X â†’áµ‡ â„` is a bounded continuous function, then there exists
a bounded continuous function `g : Y â†’áµ‡ â„` of the same norm such that `g âˆ˜ e = f`. -/
theorem exists_extension_norm_eq_of_closedEmbedding' (f : X â†’áµ‡ â„) (e : C(X, Y))
    (he : ClosedEmbedding e) : âˆƒ g : Y â†’áµ‡ â„, â€–gâ€– = â€–fâ€– âˆ§ g.compContinuous e = f := by
  /- For the proof, we iterate `tietze_extension_step`. Each time we apply it to the difference
    between the previous approximation and `f`. -/
  choose F hF_norm hF_dist using fun f : X â†’áµ‡ â„ => tietze_extension_step f e he
  -- âŠ¢ âˆƒ g, â€–gâ€– = â€–fâ€– âˆ§ compContinuous g e = f
  set g : â„• â†’ Y â†’áµ‡ â„ := fun n => (fun g => g + F (f - g.compContinuous e))^[n] 0
  -- âŠ¢ âˆƒ g, â€–gâ€– = â€–fâ€– âˆ§ compContinuous g e = f
  have g0 : g 0 = 0 := rfl
  -- âŠ¢ âˆƒ g, â€–gâ€– = â€–fâ€– âˆ§ compContinuous g e = f
  have g_succ : âˆ€ n, g (n + 1) = g n + F (f - (g n).compContinuous e) := fun n =>
    Function.iterate_succ_apply' _ _ _
  have hgf : âˆ€ n, dist ((g n).compContinuous e) f â‰¤ (2 / 3) ^ n * â€–fâ€– := by
    intro n
    induction' n with n ihn
    Â· simp [g0]
    Â· rw [g_succ n, add_compContinuous, â† dist_sub_right, add_sub_cancel', pow_succ, mul_assoc]
      refine' (hF_dist _).trans (mul_le_mul_of_nonneg_left _ (by norm_num1))
      rwa [â† dist_eq_norm']
  have hg_dist : âˆ€ n, dist (g n) (g (n + 1)) â‰¤ 1 / 3 * â€–fâ€– * (2 / 3) ^ n := by
    intro n
    calc
      dist (g n) (g (n + 1)) = â€–F (f - (g n).compContinuous e)â€– := by
        rw [g_succ, dist_eq_norm', add_sub_cancel']
      _ â‰¤ â€–f - (g n).compContinuous eâ€– / 3 := (hF_norm _)
      _ = 1 / 3 * dist ((g n).compContinuous e) f := by rw [dist_eq_norm', one_div, div_eq_inv_mul]
      _ â‰¤ 1 / 3 * ((2 / 3) ^ n * â€–fâ€–) := (mul_le_mul_of_nonneg_left (hgf n) (by norm_num1))
      _ = 1 / 3 * â€–fâ€– * (2 / 3) ^ n := by ac_rfl
  have hg_cau : CauchySeq g := cauchySeq_of_le_geometric _ _ (by norm_num1) hg_dist
  -- âŠ¢ âˆƒ g, â€–gâ€– = â€–fâ€– âˆ§ compContinuous g e = f
  have :
    Tendsto (fun n => (g n).compContinuous e) atTop
      (ğ“ <| (limUnder atTop g).compContinuous e) :=
    ((continuous_compContinuous e).tendsto _).comp hg_cau.tendsto_limUnder
  have hge : (limUnder atTop g).compContinuous e = f := by
    refine' tendsto_nhds_unique this (tendsto_iff_dist_tendsto_zero.2 _)
    refine' squeeze_zero (fun _ => dist_nonneg) hgf _
    rw [â† zero_mul â€–fâ€–]
    refine' (tendsto_pow_atTop_nhds_0_of_lt_1 _ _).mul tendsto_const_nhds <;> norm_num1
  refine' âŸ¨limUnder atTop g, le_antisymm _ _, hgeâŸ©
  -- âŠ¢ â€–limUnder atTop gâ€– â‰¤ â€–fâ€–
  Â· rw [â† dist_zero_left, â† g0]
    -- âŠ¢ dist (g 0) (limUnder atTop g) â‰¤ â€–fâ€–
    refine'
      (dist_le_of_le_geometric_of_tendstoâ‚€ _ _ (by norm_num1)
        hg_dist hg_cau.tendsto_limUnder).trans_eq _
    field_simp [show (3 - 2 : â„) = 1 by norm_num1]
    -- ğŸ‰ no goals
  Â· rw [â† hge]
    -- âŠ¢ â€–compContinuous (limUnder atTop g) eâ€– â‰¤ â€–limUnder atTop gâ€–
    exact norm_compContinuous_le _ _
    -- ğŸ‰ no goals
#align bounded_continuous_function.exists_extension_norm_eq_of_closed_embedding' BoundedContinuousFunction.exists_extension_norm_eq_of_closedEmbedding'

/-- **Tietze extension theorem** for real-valued bounded continuous maps, a version with a closed
embedding and unbundled composition. If `e : C(X, Y)` is a closed embedding of a topological space
into a normal topological space and `f : X â†’áµ‡ â„` is a bounded continuous function, then there exists
a bounded continuous function `g : Y â†’áµ‡ â„` of the same norm such that `g âˆ˜ e = f`. -/
theorem exists_extension_norm_eq_of_closedEmbedding (f : X â†’áµ‡ â„) {e : X â†’ Y}
    (he : ClosedEmbedding e) : âˆƒ g : Y â†’áµ‡ â„, â€–gâ€– = â€–fâ€– âˆ§ g âˆ˜ e = f := by
  rcases exists_extension_norm_eq_of_closedEmbedding' f âŸ¨e, he.continuousâŸ© he with âŸ¨g, hg, rflâŸ©
  -- âŠ¢ âˆƒ g_1, â€–g_1â€– = â€–compContinuous g (ContinuousMap.mk e)â€– âˆ§ â†‘g_1 âˆ˜ e = â†‘(compCo â€¦
  exact âŸ¨g, hg, rflâŸ©
  -- ğŸ‰ no goals
#align bounded_continuous_function.exists_extension_norm_eq_of_closed_embedding BoundedContinuousFunction.exists_extension_norm_eq_of_closedEmbedding

/-- **Tietze extension theorem** for real-valued bounded continuous maps, a version for a closed
set. If `f` is a bounded continuous real-valued function defined on a closed set in a normal
topological space, then it can be extended to a bounded continuous function of the same norm defined
on the whole space. -/
theorem exists_norm_eq_restrict_eq_of_closed {s : Set Y} (f : s â†’áµ‡ â„) (hs : IsClosed s) :
    âˆƒ g : Y â†’áµ‡ â„, â€–gâ€– = â€–fâ€– âˆ§ g.restrict s = f :=
  exists_extension_norm_eq_of_closedEmbedding' f ((ContinuousMap.id _).restrict s)
    (closedEmbedding_subtype_val hs)
#align bounded_continuous_function.exists_norm_eq_restrict_eq_of_closed BoundedContinuousFunction.exists_norm_eq_restrict_eq_of_closed

/-- **Tietze extension theorem** for real-valued bounded continuous maps, a version for a closed
embedding and a bounded continuous function that takes values in a non-trivial closed interval.
See also `exists_extension_forall_mem_of_closedEmbedding` for a more general statement that works
for any interval (finite or infinite, open or closed).

If `e : X â†’ Y` is a closed embedding and `f : X â†’áµ‡ â„` is a bounded continuous function such that
`f x âˆˆ [a, b]` for all `x`, where `a â‰¤ b`, then there exists a bounded continuous function
`g : Y â†’áµ‡ â„` such that `g y âˆˆ [a, b]` for all `y` and `g âˆ˜ e = f`. -/
theorem exists_extension_forall_mem_Icc_of_closedEmbedding (f : X â†’áµ‡ â„) {a b : â„} {e : X â†’ Y}
    (hf : âˆ€ x, f x âˆˆ Icc a b) (hle : a â‰¤ b) (he : ClosedEmbedding e) :
    âˆƒ g : Y â†’áµ‡ â„, (âˆ€ y, g y âˆˆ Icc a b) âˆ§ g âˆ˜ e = f := by
  rcases exists_extension_norm_eq_of_closedEmbedding (f - const X ((a + b) / 2)) he with
    âŸ¨g, hgf, hgeâŸ©
  refine' âŸ¨const Y ((a + b) / 2) + g, fun y => _, _âŸ©
  -- âŠ¢ â†‘(const Y ((a + b) / 2) + g) y âˆˆ Icc a b
  Â· suffices â€–f - const X ((a + b) / 2)â€– â‰¤ (b - a) / 2 by
      simpa [Real.Icc_eq_closedBall, add_mem_closedBall_iff_norm] using
        (norm_coe_le_norm g y).trans (hgf.trans_le this)
    refine' (norm_le <| div_nonneg (sub_nonneg.2 hle) zero_le_two).2 fun x => _
    -- âŠ¢ â€–â†‘(f - const X ((a + b) / 2)) xâ€– â‰¤ (b - a) / 2
    simpa only [Real.Icc_eq_closedBall] using hf x
    -- ğŸ‰ no goals
  Â· ext x
    -- âŠ¢ (â†‘(const Y ((a + b) / 2) + g) âˆ˜ e) x = â†‘f x
    have : g (e x) = f x - (a + b) / 2 := congr_fun hge x
    -- âŠ¢ (â†‘(const Y ((a + b) / 2) + g) âˆ˜ e) x = â†‘f x
    simp [this]
    -- ğŸ‰ no goals
#align bounded_continuous_function.exists_extension_forall_mem_Icc_of_closed_embedding BoundedContinuousFunction.exists_extension_forall_mem_Icc_of_closedEmbedding

/-- **Tietze extension theorem** for real-valued bounded continuous maps, a version for a closed
embedding. Let `e` be a closed embedding of a nonempty topological space `X` into a normal
topological space `Y`. Let `f` be a bounded continuous real-valued function on `X`. Then there
exists a bounded continuous function `g : Y â†’áµ‡ â„` such that `g âˆ˜ e = f` and each value `g y` belongs
to a closed interval `[f xâ‚, f xâ‚‚]` for some `xâ‚` and `xâ‚‚`.  -/
theorem exists_extension_forall_exists_le_ge_of_closedEmbedding [Nonempty X] (f : X â†’áµ‡ â„)
    {e : X â†’ Y} (he : ClosedEmbedding e) :
    âˆƒ g : Y â†’áµ‡ â„, (âˆ€ y, âˆƒ xâ‚ xâ‚‚, g y âˆˆ Icc (f xâ‚) (f xâ‚‚)) âˆ§ g âˆ˜ e = f := by
  inhabit X
  -- âŠ¢ âˆƒ g, (âˆ€ (y : Y), âˆƒ xâ‚ xâ‚‚, â†‘g y âˆˆ Icc (â†‘f xâ‚) (â†‘f xâ‚‚)) âˆ§ â†‘g âˆ˜ e = â†‘f
  -- Put `a = â¨… x, f x` and `b = â¨† x, f x`
  obtain âŸ¨a, haâŸ© : âˆƒ a, IsGLB (range f) a
  -- âŠ¢ âˆƒ a, IsGLB (range â†‘f) a
  exact âŸ¨_, isGLB_ciInf (Real.bounded_iff_bddBelow_bddAbove.1 f.bounded_range).1âŸ©
  -- âŠ¢ âˆƒ g, (âˆ€ (y : Y), âˆƒ xâ‚ xâ‚‚, â†‘g y âˆˆ Icc (â†‘f xâ‚) (â†‘f xâ‚‚)) âˆ§ â†‘g âˆ˜ e = â†‘f
  obtain âŸ¨b, hbâŸ© : âˆƒ b, IsLUB (range f) b
  -- âŠ¢ âˆƒ b, IsLUB (range â†‘f) b
  exact âŸ¨_, isLUB_ciSup (Real.bounded_iff_bddBelow_bddAbove.1 f.bounded_range).2âŸ©
  -- âŠ¢ âˆƒ g, (âˆ€ (y : Y), âˆƒ xâ‚ xâ‚‚, â†‘g y âˆˆ Icc (â†‘f xâ‚) (â†‘f xâ‚‚)) âˆ§ â†‘g âˆ˜ e = â†‘f
  -- Then `f x âˆˆ [a, b]` for all `x`
  have hmem : âˆ€ x, f x âˆˆ Icc a b := fun x => âŸ¨ha.1 âŸ¨x, rflâŸ©, hb.1 âŸ¨x, rflâŸ©âŸ©
  -- âŠ¢ âˆƒ g, (âˆ€ (y : Y), âˆƒ xâ‚ xâ‚‚, â†‘g y âˆˆ Icc (â†‘f xâ‚) (â†‘f xâ‚‚)) âˆ§ â†‘g âˆ˜ e = â†‘f
  -- Rule out the trivial case `a = b`
  have hle : a â‰¤ b := (hmem default).1.trans (hmem default).2
  -- âŠ¢ âˆƒ g, (âˆ€ (y : Y), âˆƒ xâ‚ xâ‚‚, â†‘g y âˆˆ Icc (â†‘f xâ‚) (â†‘f xâ‚‚)) âˆ§ â†‘g âˆ˜ e = â†‘f
  rcases hle.eq_or_lt with (rfl | hlt)
  -- âŠ¢ âˆƒ g, (âˆ€ (y : Y), âˆƒ xâ‚ xâ‚‚, â†‘g y âˆˆ Icc (â†‘f xâ‚) (â†‘f xâ‚‚)) âˆ§ â†‘g âˆ˜ e = â†‘f
  Â· have : âˆ€ x, f x = a := by simpa using hmem
    -- âŠ¢ âˆƒ g, (âˆ€ (y : Y), âˆƒ xâ‚ xâ‚‚, â†‘g y âˆˆ Icc (â†‘f xâ‚) (â†‘f xâ‚‚)) âˆ§ â†‘g âˆ˜ e = â†‘f
    use const Y a
    -- âŠ¢ (âˆ€ (y : Y), âˆƒ xâ‚ xâ‚‚, â†‘(const Y a) y âˆˆ Icc (â†‘f xâ‚) (â†‘f xâ‚‚)) âˆ§ â†‘(const Y a) âˆ˜  â€¦
    simp [this, Function.funext_iff]
    -- ğŸ‰ no goals
  -- Put `c = (a + b) / 2`. Then `a < c < b` and `c - a = b - c`.
  set c := (a + b) / 2
  -- âŠ¢ âˆƒ g, (âˆ€ (y : Y), âˆƒ xâ‚ xâ‚‚, â†‘g y âˆˆ Icc (â†‘f xâ‚) (â†‘f xâ‚‚)) âˆ§ â†‘g âˆ˜ e = â†‘f
  have hac : a < c := left_lt_add_div_two.2 hlt
  -- âŠ¢ âˆƒ g, (âˆ€ (y : Y), âˆƒ xâ‚ xâ‚‚, â†‘g y âˆˆ Icc (â†‘f xâ‚) (â†‘f xâ‚‚)) âˆ§ â†‘g âˆ˜ e = â†‘f
  have hcb : c < b := add_div_two_lt_right.2 hlt
  -- âŠ¢ âˆƒ g, (âˆ€ (y : Y), âˆƒ xâ‚ xâ‚‚, â†‘g y âˆˆ Icc (â†‘f xâ‚) (â†‘f xâ‚‚)) âˆ§ â†‘g âˆ˜ e = â†‘f
  have hsub : c - a = b - c := by
    field_simp
    ring
  /- Due to `exists_extension_forall_mem_Icc_of_closedEmbedding`, there exists an extension `g`
    such that `g y âˆˆ [a, b]` for all `y`. However, if `a` and/or `b` do not belong to the range of
    `f`, then we need to ensure that these points do not belong to the range of `g`. This is done
    in two almost identical steps. First we deal with the case `âˆ€ x, f x â‰  a`. -/
  obtain âŸ¨g, hg_mem, hgfâŸ© : âˆƒ g : Y â†’áµ‡ â„, (âˆ€ y, âˆƒ x, g y âˆˆ Icc (f x) b) âˆ§ g âˆ˜ e = f := by
    rcases exists_extension_forall_mem_Icc_of_closedEmbedding f hmem hle he with âŸ¨g, hg_mem, hgfâŸ©
    -- If `a âˆˆ range f`, then we are done.
    rcases em (âˆƒ x, f x = a) with (âŸ¨x, rflâŸ© | ha')
    Â· exact âŸ¨g, fun y => âŸ¨x, hg_mem _âŸ©, hgfâŸ©
    /- Otherwise, `g â»Â¹' {a}` is disjoint with `range e âˆª g â»Â¹' (Ici c)`, hence there exists a
        function `dg : Y â†’ â„` such that `dg âˆ˜ e = 0`, `dg y = 0` whenever `c â‰¤ g y`, `dg y = c - a`
        whenever `g y = a`, and `0 â‰¤ dg y â‰¤ c - a` for all `y`.  -/
    have hd : Disjoint (range e âˆª g â»Â¹' Ici c) (g â»Â¹' {a}) := by
      refine' disjoint_union_left.2 âŸ¨_, Disjoint.preimage _ _âŸ©
      Â· rw [Set.disjoint_left]
        rintro _ âŸ¨x, rflâŸ© (rfl : g (e x) = a)
        exact ha' âŸ¨x, (congr_fun hgf x).symmâŸ©
      Â· exact Set.disjoint_singleton_right.2 hac.not_le
    rcases exists_bounded_mem_Icc_of_closed_of_le
        (he.closed_range.union <| isClosed_Ici.preimage g.continuous)
        (isClosed_singleton.preimage g.continuous) hd (sub_nonneg.2 hac.le) with
      âŸ¨dg, dg0, dga, dgmemâŸ©
    replace hgf : âˆ€ x, (g + dg) (e x) = f x
    Â· intro x
      simp [dg0 (Or.inl <| mem_range_self _), â† hgf]
    refine' âŸ¨g + dg, fun y => _, funext hgfâŸ©
    Â· have hay : a < (g + dg) y := by
        rcases(hg_mem y).1.eq_or_lt with (rfl | hlt)
        Â· refine' (lt_add_iff_pos_right _).2 _
          calc
            0 < c - g y := sub_pos.2 hac
            _ = dg y := (dga rfl).symm
        Â· exact hlt.trans_le ((le_add_iff_nonneg_right _).2 <| (dgmem y).1)
      rcases ha.exists_between hay with âŸ¨_, âŸ¨x, rflâŸ©, _, hxyâŸ©
      refine' âŸ¨x, hxy.le, _âŸ©
      cases' le_total c (g y) with hc hc
      Â· simp [dg0 (Or.inr hc), (hg_mem y).2]
      Â· calc
          g y + dg y â‰¤ c + (c - a) := add_le_add hc (dgmem _).2
          _ = b := by rw [hsub, add_sub_cancel'_right]
  /- Now we deal with the case `âˆ€ x, f x â‰  b`. The proof is the same as in the first case, with
    minor modifications that make it hard to deduplicate code. -/
  choose xl hxl hgb using hg_mem
  -- âŠ¢ âˆƒ g, (âˆ€ (y : Y), âˆƒ xâ‚ xâ‚‚, â†‘g y âˆˆ Icc (â†‘f xâ‚) (â†‘f xâ‚‚)) âˆ§ â†‘g âˆ˜ e = â†‘f
  rcases em (âˆƒ x, f x = b) with (âŸ¨x, rflâŸ© | hb')
  -- âŠ¢ âˆƒ g, (âˆ€ (y : Y), âˆƒ xâ‚ xâ‚‚, â†‘g y âˆˆ Icc (â†‘f xâ‚) (â†‘f xâ‚‚)) âˆ§ â†‘g âˆ˜ e = â†‘f
  Â· exact âŸ¨g, fun y => âŸ¨xl y, x, hxl y, hgb yâŸ©, hgfâŸ©
    -- ğŸ‰ no goals
  have hd : Disjoint (range e âˆª g â»Â¹' Iic c) (g â»Â¹' {b}) := by
    refine' disjoint_union_left.2 âŸ¨_, Disjoint.preimage _ _âŸ©
    Â· rw [Set.disjoint_left]
      rintro _ âŸ¨x, rflâŸ© (rfl : g (e x) = b)
      exact hb' âŸ¨x, (congr_fun hgf x).symmâŸ©
    Â· exact Set.disjoint_singleton_right.2 hcb.not_le
  rcases exists_bounded_mem_Icc_of_closed_of_le
      (he.closed_range.union <| isClosed_Iic.preimage g.continuous)
      (isClosed_singleton.preimage g.continuous) hd (sub_nonneg.2 hcb.le) with
    âŸ¨dg, dg0, dgb, dgmemâŸ©
  replace hgf : âˆ€ x, (g - dg) (e x) = f x
  -- âŠ¢ âˆ€ (x : X), â†‘(g - dg) (e x) = â†‘f x
  Â· intro x
    -- âŠ¢ â†‘(g - dg) (e x) = â†‘f x
    simp [dg0 (Or.inl <| mem_range_self _), â† hgf]
    -- ğŸ‰ no goals
  refine' âŸ¨g - dg, fun y => _, funext hgfâŸ©
  -- âŠ¢ âˆƒ xâ‚ xâ‚‚, â†‘(g - dg) y âˆˆ Icc (â†‘f xâ‚) (â†‘f xâ‚‚)
  Â· have hyb : (g - dg) y < b := by
      rcases(hgb y).eq_or_lt with (rfl | hlt)
      Â· refine' (sub_lt_self_iff _).2 _
        calc
          0 < g y - c := sub_pos.2 hcb
          _ = dg y := (dgb rfl).symm
      Â· exact ((sub_le_self_iff _).2 (dgmem _).1).trans_lt hlt
    rcases hb.exists_between hyb with âŸ¨_, âŸ¨xu, rflâŸ©, hyxu, _âŸ©
    -- âŠ¢ âˆƒ xâ‚ xâ‚‚, â†‘(g - dg) y âˆˆ Icc (â†‘f xâ‚) (â†‘f xâ‚‚)
    cases' lt_or_le c (g y) with hc hc
    -- âŠ¢ âˆƒ xâ‚ xâ‚‚, â†‘(g - dg) y âˆˆ Icc (â†‘f xâ‚) (â†‘f xâ‚‚)
    Â· rcases em (a âˆˆ range f) with (âŸ¨x, rflâŸ© | _)
      -- âŠ¢ âˆƒ xâ‚ xâ‚‚, â†‘(g - dg) y âˆˆ Icc (â†‘f xâ‚) (â†‘f xâ‚‚)
      Â· refine' âŸ¨x, xu, _, hyxu.leâŸ©
        -- âŠ¢ â†‘f x â‰¤ â†‘(g - dg) y
        calc
          f x = c - (b - c) := by rw [â† hsub, sub_sub_cancel]
          _ â‰¤ g y - dg y := sub_le_sub hc.le (dgmem _).2
      Â· have hay : a < (g - dg) y := by
          calc
            a = c - (b - c) := by rw [â† hsub, sub_sub_cancel]
            _ < g y - (b - c) := (sub_lt_sub_right hc _)
            _ â‰¤ g y - dg y := sub_le_sub_left (dgmem _).2 _
        rcases ha.exists_between hay with âŸ¨_, âŸ¨x, rflâŸ©, _, hxyâŸ©
        -- âŠ¢ âˆƒ xâ‚ xâ‚‚, â†‘(g - dg) y âˆˆ Icc (â†‘f xâ‚) (â†‘f xâ‚‚)
        exact âŸ¨x, xu, hxy.le, hyxu.leâŸ©
        -- ğŸ‰ no goals
    Â· refine' âŸ¨xl y, xu, _, hyxu.leâŸ©
      -- âŠ¢ â†‘f (xl y) â‰¤ â†‘(g - dg) y
      simp [dg0 (Or.inr hc), hxl]
      -- ğŸ‰ no goals
#align bounded_continuous_function.exists_extension_forall_exists_le_ge_of_closed_embedding BoundedContinuousFunction.exists_extension_forall_exists_le_ge_of_closedEmbedding

/-- **Tietze extension theorem** for real-valued bounded continuous maps, a version for a closed
embedding. Let `e` be a closed embedding of a nonempty topological space `X` into a normal
topological space `Y`. Let `f` be a bounded continuous real-valued function on `X`. Let `t` be
a nonempty convex set of real numbers (we use `OrdConnected` instead of `Convex` to automatically
deduce this argument by typeclass search) such that `f x âˆˆ t` for all `x`. Then there exists
a bounded continuous real-valued function `g : Y â†’áµ‡ â„` such that `g y âˆˆ t` for all `y` and
`g âˆ˜ e = f`. -/
theorem exists_extension_forall_mem_of_closedEmbedding (f : X â†’áµ‡ â„) {t : Set â„} {e : X â†’ Y}
    [hs : OrdConnected t] (hf : âˆ€ x, f x âˆˆ t) (hne : t.Nonempty) (he : ClosedEmbedding e) :
    âˆƒ g : Y â†’áµ‡ â„, (âˆ€ y, g y âˆˆ t) âˆ§ g âˆ˜ e = f := by
  cases isEmpty_or_nonempty X
  -- âŠ¢ âˆƒ g, (âˆ€ (y : Y), â†‘g y âˆˆ t) âˆ§ â†‘g âˆ˜ e = â†‘f
  Â· rcases hne with âŸ¨c, hcâŸ©
    -- âŠ¢ âˆƒ g, (âˆ€ (y : Y), â†‘g y âˆˆ t) âˆ§ â†‘g âˆ˜ e = â†‘f
    refine' âŸ¨const Y c, fun _ => hc, funext fun x => isEmptyElim xâŸ©
    -- ğŸ‰ no goals
  rcases exists_extension_forall_exists_le_ge_of_closedEmbedding f he with âŸ¨g, hg, hgfâŸ©
  -- âŠ¢ âˆƒ g, (âˆ€ (y : Y), â†‘g y âˆˆ t) âˆ§ â†‘g âˆ˜ e = â†‘f
  refine' âŸ¨g, fun y => _, hgfâŸ©
  -- âŠ¢ â†‘g y âˆˆ t
  rcases hg y with âŸ¨xl, xu, hâŸ©
  -- âŠ¢ â†‘g y âˆˆ t
  exact hs.out (hf _) (hf _) h
  -- ğŸ‰ no goals
#align bounded_continuous_function.exists_extension_forall_mem_of_closed_embedding BoundedContinuousFunction.exists_extension_forall_mem_of_closedEmbedding

/-- **Tietze extension theorem** for real-valued bounded continuous maps, a version for a closed
set. Let `s` be a closed set in a normal topological space `Y`. Let `f` be a bounded continuous
real-valued function on `s`. Let `t` be a nonempty convex set of real numbers (we use
`OrdConnected` instead of `Convex` to automatically deduce this argument by typeclass search) such
that `f x âˆˆ t` for all `x : s`. Then there exists a bounded continuous real-valued function
`g : Y â†’áµ‡ â„` such that `g y âˆˆ t` for all `y` and `g.restrict s = f`. -/
theorem exists_forall_mem_restrict_eq_of_closed {s : Set Y} (f : s â†’áµ‡ â„) (hs : IsClosed s)
    {t : Set â„} [OrdConnected t] (hf : âˆ€ x, f x âˆˆ t) (hne : t.Nonempty) :
    âˆƒ g : Y â†’áµ‡ â„, (âˆ€ y, g y âˆˆ t) âˆ§ g.restrict s = f := by
  rcases exists_extension_forall_mem_of_closedEmbedding f hf hne
      (closedEmbedding_subtype_val hs) with
    âŸ¨g, hg, hgfâŸ©
  exact âŸ¨g, hg, FunLike.coe_injective hgfâŸ©
  -- ğŸ‰ no goals
#align bounded_continuous_function.exists_forall_mem_restrict_eq_of_closed BoundedContinuousFunction.exists_forall_mem_restrict_eq_of_closed

end BoundedContinuousFunction

namespace ContinuousMap

/-- **Tietze extension theorem** for real-valued continuous maps, a version for a closed
embedding. Let `e` be a closed embedding of a nonempty topological space `X` into a normal
topological space `Y`. Let `f` be a continuous real-valued function on `X`. Let `t` be a nonempty
convex set of real numbers (we use `OrdConnected` instead of `Convex` to automatically deduce this
argument by typeclass search) such that `f x âˆˆ t` for all `x`. Then there exists a continuous
real-valued function `g : C(Y, â„)` such that `g y âˆˆ t` for all `y` and `g âˆ˜ e = f`. -/
theorem exists_extension_forall_mem_of_closedEmbedding (f : C(X, â„)) {t : Set â„} {e : X â†’ Y}
    [hs : OrdConnected t] (hf : âˆ€ x, f x âˆˆ t) (hne : t.Nonempty) (he : ClosedEmbedding e) :
    âˆƒ g : C(Y, â„), (âˆ€ y, g y âˆˆ t) âˆ§ g âˆ˜ e = f := by
  have h : â„ â‰ƒo Ioo (-1 : â„) 1 := orderIsoIooNegOneOne â„
  -- âŠ¢ âˆƒ g, (âˆ€ (y : Y), â†‘g y âˆˆ t) âˆ§ â†‘g âˆ˜ e = â†‘f
  let F : X â†’áµ‡ â„ :=
    { toFun := (â†‘) âˆ˜ h âˆ˜ f
      continuous_toFun := continuous_subtype_val.comp (h.continuous.comp f.continuous)
      map_bounded' :=
        bounded_range_iff.1
          ((bounded_Ioo (-1 : â„) 1).mono <| forall_range_iff.2 fun x => (h (f x)).2) }
  let t' : Set â„ := (â†‘) âˆ˜ h '' t
  -- âŠ¢ âˆƒ g, (âˆ€ (y : Y), â†‘g y âˆˆ t) âˆ§ â†‘g âˆ˜ e = â†‘f
  have ht_sub : t' âŠ† Ioo (-1 : â„) 1 := image_subset_iff.2 fun x _ => (h x).2
  -- âŠ¢ âˆƒ g, (âˆ€ (y : Y), â†‘g y âˆˆ t) âˆ§ â†‘g âˆ˜ e = â†‘f
  have : OrdConnected t' := by
    constructor
    rintro _ âŸ¨x, hx, rflâŸ© _ âŸ¨y, hy, rflâŸ© z hz
    lift z to Ioo (-1 : â„) 1 using Icc_subset_Ioo (h x).2.1 (h y).2.2 hz
    change z âˆˆ Icc (h x) (h y) at hz
    rw [â† h.image_Icc] at hz
    rcases hz with âŸ¨z, hz, rflâŸ©
    exact âŸ¨z, hs.out hx hy hz, rflâŸ©
  have hFt : âˆ€ x, F x âˆˆ t' := fun x => mem_image_of_mem _ (hf x)
  -- âŠ¢ âˆƒ g, (âˆ€ (y : Y), â†‘g y âˆˆ t) âˆ§ â†‘g âˆ˜ e = â†‘f
  rcases F.exists_extension_forall_mem_of_closedEmbedding hFt (hne.image _) he with âŸ¨G, hG, hGFâŸ©
  -- âŠ¢ âˆƒ g, (âˆ€ (y : Y), â†‘g y âˆˆ t) âˆ§ â†‘g âˆ˜ e = â†‘f
  let g : C(Y, â„) :=
    âŸ¨h.symm âˆ˜ codRestrict G _ fun y => ht_sub (hG y),
      h.symm.continuous.comp <| G.continuous.subtype_mk _âŸ©
  have hgG : âˆ€ {y a}, g y = a â†” G y = h a := @fun y a =>
    h.toEquiv.symm_apply_eq.trans Subtype.ext_iff
  refine' âŸ¨g, fun y => _, _âŸ©
  -- âŠ¢ â†‘g y âˆˆ t
  Â· rcases hG y with âŸ¨a, ha, hayâŸ©
    -- âŠ¢ â†‘g y âˆˆ t
    convert ha
    -- âŠ¢ â†‘g y = a
    exact hgG.2 hay.symm
    -- ğŸ‰ no goals
  Â· ext x
    -- âŠ¢ (â†‘g âˆ˜ e) x = â†‘f x
    exact hgG.2 (congr_fun hGF _)
    -- ğŸ‰ no goals
#align continuous_map.exists_extension_forall_mem_of_closed_embedding ContinuousMap.exists_extension_forall_mem_of_closedEmbedding

/-- **Tietze extension theorem** for real-valued continuous maps, a version for a closed
embedding. Let `e` be a closed embedding of a nonempty topological space `X` into a normal
topological space `Y`. Let `f` be a continuous real-valued function on `X`. Then there exists a
continuous real-valued function `g : C(Y, â„)` such that `g âˆ˜ e = f`. -/
theorem exists_extension_of_closedEmbedding (f : C(X, â„)) (e : X â†’ Y) (he : ClosedEmbedding e) :
    âˆƒ g : C(Y, â„), g âˆ˜ e = f :=
  (exists_extension_forall_mem_of_closedEmbedding f (fun _ => mem_univ _) univ_nonempty he).imp
    fun _ => And.right
#align continuous_map.exists_extension_of_closed_embedding ContinuousMap.exists_extension_of_closedEmbedding

/-- **Tietze extension theorem** for real-valued continuous maps, a version for a closed set. Let
`s` be a closed set in a normal topological space `Y`. Let `f` be a continuous real-valued function
on `s`. Let `t` be a nonempty convex set of real numbers (we use `ord_connected` instead of `convex`
to automatically deduce this argument by typeclass search) such that `f x âˆˆ t` for all `x : s`. Then
there exists a continuous real-valued function `g : C(Y, â„)` such that `g y âˆˆ t` for all `y` and
`g.restrict s = f`. -/
theorem exists_restrict_eq_forall_mem_of_closed {s : Set Y} (f : C(s, â„)) {t : Set â„}
    [OrdConnected t] (ht : âˆ€ x, f x âˆˆ t) (hne : t.Nonempty) (hs : IsClosed s) :
    âˆƒ g : C(Y, â„), (âˆ€ y, g y âˆˆ t) âˆ§ g.restrict s = f :=
  let âŸ¨g, hgt, hgfâŸ© :=
    exists_extension_forall_mem_of_closedEmbedding f ht hne (closedEmbedding_subtype_val hs)
  âŸ¨g, hgt, coe_injective hgfâŸ©
#align continuous_map.exists_restrict_eq_forall_mem_of_closed ContinuousMap.exists_restrict_eq_forall_mem_of_closed

/-- **Tietze extension theorem** for real-valued continuous maps, a version for a closed set. Let
`s` be a closed set in a normal topological space `Y`. Let `f` be a continuous real-valued function
on `s`. Then there exists a continuous real-valued function `g : C(Y, â„)` such that
`g.restrict s = f`. -/
theorem exists_restrict_eq_of_closed {s : Set Y} (f : C(s, â„)) (hs : IsClosed s) :
    âˆƒ g : C(Y, â„), g.restrict s = f :=
  let âŸ¨g, _, hgfâŸ© :=
    exists_restrict_eq_forall_mem_of_closed f (fun _ => mem_univ _) univ_nonempty hs
  âŸ¨g, hgfâŸ©
#align continuous_map.exists_restrict_eq_of_closed ContinuousMap.exists_restrict_eq_of_closed

end ContinuousMap
