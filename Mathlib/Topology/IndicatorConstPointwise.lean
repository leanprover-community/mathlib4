/-
Copyright (c) 2023 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import Mathlib.Algebra.Notation.Indicator
import Mathlib.Topology.Separation.Basic

/-!
# Pointwise convergence of indicator functions

In this file, we prove the equivalence of three different ways to phrase that the indicator
functions of sets converge pointwise.

## Main results

For `A` a set, `(Asáµ¢)` an indexed collection of sets, under mild conditions, the following are
equivalent:

 (a) the indicator functions of `Asáµ¢` tend to the indicator function of `A` pointwise;

 (b) for every `x`, we eventually have that `x âˆˆ Asáµ¢` holds iff `x âˆˆ A` holds;

 (c) `Tendsto As _ <| Filter.pi (pure <| Â· âˆˆ A)`.

The results stating these in the case when the indicators take values in a FrÃ©chet space are:
* `tendsto_indicator_const_iff_forall_eventually` is the equivalence (a) â†” (b);
* `tendsto_indicator_const_iff_tendsto_pi_pure` is the equivalence (a) â†” (c).

-/


open Filter Topology

variable {Î± : Type*} {A : Set Î±}
variable {Î² : Type*} [Zero Î²] [TopologicalSpace Î²]
variable {Î¹ : Type*} (L : Filter Î¹) {As : Î¹ â†’ Set Î±}

lemma tendsto_ite {Î² : Type*} {p : Î¹ â†’ Prop} [DecidablePred p] {q : Prop} [Decidable q]
    {a b : Î²} {F G : Filter Î²}
    (haG : {a}á¶œ âˆˆ G) (hbF : {b}á¶œ âˆˆ F) (haF : principal {a} â‰¤ F) (hbG : principal {b} â‰¤ G) :
    Tendsto (fun i â†¦ if p i then a else b) L (if q then F else G) â†” âˆ€á¶  i in L, p i â†” q := by
  constructor <;> intro h
  Â· by_cases hq : q
    Â· simp only [hq, ite_true] at h
      filter_upwards [mem_map.mp (h hbF)] with i hi
      simp only [Set.preimage_compl, Set.mem_compl_iff, Set.mem_preimage, Set.mem_singleton_iff,
        ite_eq_right_iff, not_forall, exists_prop] at hi
      tauto
    Â· simp only [hq, ite_false] at h
      filter_upwards [mem_map.mp (h haG)] with i hi
      simp only [Set.preimage_compl, Set.mem_compl_iff, Set.mem_preimage, Set.mem_singleton_iff,
        ite_eq_left_iff, not_forall, exists_prop] at hi
      tauto
  Â· have obs : (fun _ â†¦ if q then a else b) =á¶ [L] (fun i â†¦ if p i then a else b) := by
      filter_upwards [h] with i hi
      simp only [hi]
    apply Tendsto.congr' obs
    by_cases hq : q
    Â· simp only [hq, ite_true]
      apply le_trans _ haF
      simp only [principal_singleton, le_pure_iff, mem_map, Set.mem_singleton_iff,
        Set.preimage_const_of_mem, univ_mem]
    Â· simp only [hq, ite_false]
      apply le_trans _ hbG
      simp only [principal_singleton, le_pure_iff, mem_map, Set.mem_singleton_iff,
        Set.preimage_const_of_mem, univ_mem]

lemma tendsto_indicator_const_apply_iff_eventually' (b : Î²)
    (nhds_b : {0}á¶œ âˆˆ ğ“ b) (nhds_o : {b}á¶œ âˆˆ ğ“ 0) (x : Î±) :
    Tendsto (fun i â†¦ (As i).indicator (fun (_ : Î±) â†¦ b) x) L (ğ“ (A.indicator (fun (_ : Î±) â†¦ b) x))
      â†” âˆ€á¶  i in L, (x âˆˆ As i â†” x âˆˆ A) := by
  classical
  have heart := @tendsto_ite Î¹ L Î² (fun i â†¦ x âˆˆ As i) _ (x âˆˆ A) _ b 0 (ğ“ b) (ğ“ (0 : Î²))
                nhds_o nhds_b ?_ ?_
  Â· convert heart
    by_cases hxA : x âˆˆ A <;> simp [hxA]
  Â· simp only [principal_singleton, le_def, mem_pure]
    exact fun s s_nhds â†¦ mem_of_mem_nhds s_nhds
  Â· simp only [principal_singleton, le_def, mem_pure]
    exact fun s s_nhds â†¦ mem_of_mem_nhds s_nhds

lemma tendsto_indicator_const_iff_forall_eventually'
    (b : Î²) (nhds_b : {0}á¶œ âˆˆ ğ“ b) (nhds_o : {b}á¶œ âˆˆ ğ“ 0) :
    Tendsto (fun i â†¦ (As i).indicator (fun (_ : Î±) â†¦ b)) L (ğ“ (A.indicator (fun (_ : Î±) â†¦ b)))
      â†” âˆ€ x, âˆ€á¶  i in L, (x âˆˆ As i â†” x âˆˆ A) := by
  simp_rw [tendsto_pi_nhds]
  apply forall_congr'
  exact tendsto_indicator_const_apply_iff_eventually' L b nhds_b nhds_o

/-- The indicator functions of `Asáµ¢` evaluated at `x` tend to the indicator function of `A`
evaluated at `x` if and only if we eventually have the equivalence `x âˆˆ Asáµ¢ â†” x âˆˆ A`. -/
@[simp] lemma tendsto_indicator_const_apply_iff_eventually [T1Space Î²] (b : Î²) [NeZero b]
    (x : Î±) :
    Tendsto (fun i â†¦ (As i).indicator (fun (_ : Î±) â†¦ b) x) L (ğ“ (A.indicator (fun (_ : Î±) â†¦ b) x))
      â†” âˆ€á¶  i in L, (x âˆˆ As i â†” x âˆˆ A) := by
  apply tendsto_indicator_const_apply_iff_eventually' _ b
  Â· simp only [compl_singleton_mem_nhds_iff, ne_eq, NeZero.ne, not_false_eq_true]
  Â· simp only [compl_singleton_mem_nhds_iff, ne_eq, (NeZero.ne b).symm, not_false_eq_true]

/-- The indicator functions of `Asáµ¢` tend to the indicator function of `A` pointwise if and only if
for every `x`, we eventually have the equivalence `x âˆˆ Asáµ¢ â†” x âˆˆ A`. -/
@[simp] lemma tendsto_indicator_const_iff_forall_eventually [T1Space Î²] (b : Î²) [NeZero b] :
    Tendsto (fun i â†¦ (As i).indicator (fun (_ : Î±) â†¦ b)) L (ğ“ (A.indicator (fun (_ : Î±) â†¦ b)))
      â†” âˆ€ x, âˆ€á¶  i in L, (x âˆˆ As i â†” x âˆˆ A) := by
  apply tendsto_indicator_const_iff_forall_eventually' _ b
  Â· simp only [compl_singleton_mem_nhds_iff, ne_eq, NeZero.ne, not_false_eq_true]
  Â· simp only [compl_singleton_mem_nhds_iff, ne_eq, (NeZero.ne b).symm, not_false_eq_true]

lemma tendsto_indicator_const_iff_tendsto_pi_pure'
    (b : Î²) (nhds_b : {0}á¶œ âˆˆ ğ“ b) (nhds_o : {b}á¶œ âˆˆ ğ“ 0) :
    Tendsto (fun i â†¦ (As i).indicator (fun (_ : Î±) â†¦ b)) L (ğ“ (A.indicator (fun (_ : Î±) â†¦ b)))
      â†” (Tendsto As L <| Filter.pi (pure <| Â· âˆˆ A)) := by
  rw [tendsto_indicator_const_iff_forall_eventually' _ b nhds_b nhds_o, tendsto_pi]
  simp_rw [tendsto_pure]
  aesop

lemma tendsto_indicator_const_iff_tendsto_pi_pure [T1Space Î²] (b : Î²) [NeZero b] :
    Tendsto (fun i â†¦ (As i).indicator (fun (_ : Î±) â†¦ b)) L (ğ“ (A.indicator (fun (_ : Î±) â†¦ b)))
      â†” (Tendsto As L <| Filter.pi (pure <| Â· âˆˆ A)) := by
  rw [tendsto_indicator_const_iff_forall_eventually _ b, tendsto_pi]
  simp_rw [tendsto_pure]
  aesop
