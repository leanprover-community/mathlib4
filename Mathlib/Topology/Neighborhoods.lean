/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Jeremy Avigad
-/
import Mathlib.Order.Filter.AtTopBot.Basic
import Mathlib.Topology.Closure

/-!
# Neighborhoods in topological spaces

Each point `x` of `X` gets a neighborhood filter `ğ“ x`.

## Tags

neighborhood
-/

open Set Filter Topology

universe u v

variable {X : Type u} [TopologicalSpace X] {Î¹ : Sort v} {Î± : Type*} {x : X} {s t : Set X}

theorem nhds_def' (x : X) : ğ“ x = â¨… (s : Set X) (_ : IsOpen s) (_ : x âˆˆ s), ğ“Ÿ s := by
  simp only [nhds_def, mem_setOf_eq, @and_comm (x âˆˆ _), iInf_and]

/-- The open sets containing `x` are a basis for the neighborhood filter. See `nhds_basis_opens'`
for a variant using open neighborhoods instead. -/
theorem nhds_basis_opens (x : X) :
    (ğ“ x).HasBasis (fun s : Set X => x âˆˆ s âˆ§ IsOpen s) fun s => s := by
  rw [nhds_def]
  exact hasBasis_biInf_principal
    (fun s âŸ¨has, hsâŸ© t âŸ¨hat, htâŸ© =>
      âŸ¨s âˆ© t, âŸ¨âŸ¨has, hatâŸ©, IsOpen.inter hs htâŸ©, âŸ¨inter_subset_left, inter_subset_rightâŸ©âŸ©)
    âŸ¨univ, âŸ¨mem_univ x, isOpen_univâŸ©âŸ©

theorem nhds_basis_closeds (x : X) : (ğ“ x).HasBasis (fun s : Set X => x âˆ‰ s âˆ§ IsClosed s) compl :=
  âŸ¨fun t => (nhds_basis_opens x).mem_iff.trans <|
    compl_surjective.exists.trans <| by simp only [isOpen_compl_iff, mem_compl_iff]âŸ©

@[simp]
theorem lift'_nhds_interior (x : X) : (ğ“ x).lift' interior = ğ“ x :=
  (nhds_basis_opens x).lift'_interior_eq_self fun _ â†¦ And.right

theorem Filter.HasBasis.nhds_interior {x : X} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set X}
    (h : (ğ“ x).HasBasis p s) : (ğ“ x).HasBasis p (interior <| s Â·) :=
  lift'_nhds_interior x â–¸ h.lift'_interior

/-- A filter lies below the neighborhood filter at `x` iff it contains every open set around `x`. -/
theorem le_nhds_iff {f} : f â‰¤ ğ“ x â†” âˆ€ s : Set X, x âˆˆ s â†’ IsOpen s â†’ s âˆˆ f := by simp [nhds_def]

/-- To show a filter is above the neighborhood filter at `x`, it suffices to show that it is above
the principal filter of some open set `s` containing `x`. -/
theorem nhds_le_of_le {f} (h : x âˆˆ s) (o : IsOpen s) (sf : ğ“Ÿ s â‰¤ f) : ğ“ x â‰¤ f := by
  rw [nhds_def]; exact iInfâ‚‚_le_of_le s âŸ¨h, oâŸ© sf

theorem mem_nhds_iff : s âˆˆ ğ“ x â†” âˆƒ t âŠ† s, IsOpen t âˆ§ x âˆˆ t :=
  (nhds_basis_opens x).mem_iff.trans <| exists_congr fun _ =>
    âŸ¨fun h => âŸ¨h.2, h.1.2, h.1.1âŸ©, fun h => âŸ¨âŸ¨h.2.2, h.2.1âŸ©, h.1âŸ©âŸ©

/-- A predicate is true in a neighborhood of `x` iff it is true for all the points in an open set
containing `x`. -/
theorem eventually_nhds_iff {p : X â†’ Prop} :
    (âˆ€á¶  y in ğ“ x, p y) â†” âˆƒ t : Set X, (âˆ€ y âˆˆ t, p y) âˆ§ IsOpen t âˆ§ x âˆˆ t :=
  mem_nhds_iff.trans <| by simp only [subset_def, exists_prop, mem_setOf_eq]

theorem frequently_nhds_iff {p : X â†’ Prop} :
    (âˆƒá¶  y in ğ“ x, p y) â†” âˆ€ U : Set X, x âˆˆ U â†’ IsOpen U â†’ âˆƒ y âˆˆ U, p y :=
  (nhds_basis_opens x).frequently_iff.trans <| by simp

theorem mem_interior_iff_mem_nhds : x âˆˆ interior s â†” s âˆˆ ğ“ x :=
  mem_interior.trans mem_nhds_iff.symm

theorem map_nhds {f : X â†’ Î±} :
    map f (ğ“ x) = â¨… s âˆˆ { s : Set X | x âˆˆ s âˆ§ IsOpen s }, ğ“Ÿ (f '' s) :=
  ((nhds_basis_opens x).map f).eq_biInf

theorem mem_of_mem_nhds : s âˆˆ ğ“ x â†’ x âˆˆ s := fun H =>
  let âŸ¨_t, ht, _, hsâŸ© := mem_nhds_iff.1 H; ht hs

/-- If a predicate is true in a neighborhood of `x`, then it is true for `x`. -/
theorem Filter.Eventually.self_of_nhds {p : X â†’ Prop} (h : âˆ€á¶  y in ğ“ x, p y) : p x :=
  mem_of_mem_nhds h

theorem IsOpen.mem_nhds (hs : IsOpen s) (hx : x âˆˆ s) : s âˆˆ ğ“ x :=
  mem_nhds_iff.2 âŸ¨s, Subset.refl _, hs, hxâŸ©

protected theorem IsOpen.mem_nhds_iff (hs : IsOpen s) : s âˆˆ ğ“ x â†” x âˆˆ s :=
  âŸ¨mem_of_mem_nhds, fun hx => mem_nhds_iff.2 âŸ¨s, Subset.rfl, hs, hxâŸ©âŸ©

theorem IsClosed.compl_mem_nhds (hs : IsClosed s) (hx : x âˆ‰ s) : sá¶œ âˆˆ ğ“ x :=
  hs.isOpen_compl.mem_nhds (mem_compl hx)

theorem IsOpen.eventually_mem (hs : IsOpen s) (hx : x âˆˆ s) :
    âˆ€á¶  x in ğ“ x, x âˆˆ s :=
  IsOpen.mem_nhds hs hx

/-- The open neighborhoods of `x` are a basis for the neighborhood filter. See `nhds_basis_opens`
for a variant using open sets around `x` instead. -/
theorem nhds_basis_opens' (x : X) :
    (ğ“ x).HasBasis (fun s : Set X => s âˆˆ ğ“ x âˆ§ IsOpen s) fun x => x := by
  convert nhds_basis_opens x using 2
  exact and_congr_left_iff.2 IsOpen.mem_nhds_iff

/-- If `U` is a neighborhood of each point of a set `s` then it is a neighborhood of `s`:
it contains an open set containing `s`. -/
theorem exists_open_set_nhds {U : Set X} (h : âˆ€ x âˆˆ s, U âˆˆ ğ“ x) :
    âˆƒ V : Set X, s âŠ† V âˆ§ IsOpen V âˆ§ V âŠ† U :=
  âŸ¨interior U, fun x hx => mem_interior_iff_mem_nhds.2 <| h x hx, isOpen_interior, interior_subsetâŸ©

/-- If `U` is a neighborhood of each point of a set `s` then it is a neighborhood of s:
it contains an open set containing `s`. -/
theorem exists_open_set_nhds' {U : Set X} (h : U âˆˆ â¨† x âˆˆ s, ğ“ x) :
    âˆƒ V : Set X, s âŠ† V âˆ§ IsOpen V âˆ§ V âŠ† U :=
  exists_open_set_nhds (by simpa using h)

/-- If a predicate is true in a neighbourhood of `x`, then for `y` sufficiently close
to `x` this predicate is true in a neighbourhood of `y`. -/
theorem Filter.Eventually.eventually_nhds {p : X â†’ Prop} (h : âˆ€á¶  y in ğ“ x, p y) :
    âˆ€á¶  y in ğ“ x, âˆ€á¶  x in ğ“ y, p x :=
  let âŸ¨t, htp, hto, haâŸ© := eventually_nhds_iff.1 h
  eventually_nhds_iff.2 âŸ¨t, fun _x hx => eventually_nhds_iff.2 âŸ¨t, htp, hto, hxâŸ©, hto, haâŸ©

@[simp]
theorem eventually_eventually_nhds {p : X â†’ Prop} :
    (âˆ€á¶  y in ğ“ x, âˆ€á¶  x in ğ“ y, p x) â†” âˆ€á¶  x in ğ“ x, p x :=
  âŸ¨fun h => h.self_of_nhds, fun h => h.eventually_nhdsâŸ©

@[simp]
theorem frequently_frequently_nhds {p : X â†’ Prop} :
    (âˆƒá¶  x' in ğ“ x, âˆƒá¶  x'' in ğ“ x', p x'') â†” âˆƒá¶  x in ğ“ x, p x := by
  rw [â† not_iff_not]
  simp only [not_frequently, eventually_eventually_nhds]

@[simp]
theorem eventually_mem_nhds_iff : (âˆ€á¶  x' in ğ“ x, s âˆˆ ğ“ x') â†” s âˆˆ ğ“ x :=
  eventually_eventually_nhds

@[simp]
theorem nhds_bind_nhds : (ğ“ x).bind ğ“ = ğ“ x :=
  Filter.ext fun _ => eventually_eventually_nhds

@[simp]
theorem eventually_eventuallyEq_nhds {f g : X â†’ Î±} :
    (âˆ€á¶  y in ğ“ x, f =á¶ [ğ“ y] g) â†” f =á¶ [ğ“ x] g :=
  eventually_eventually_nhds

theorem Filter.EventuallyEq.eq_of_nhds {f g : X â†’ Î±} (h : f =á¶ [ğ“ x] g) : f x = g x :=
  h.self_of_nhds

@[simp]
theorem eventually_eventuallyLE_nhds [LE Î±] {f g : X â†’ Î±} :
    (âˆ€á¶  y in ğ“ x, f â‰¤á¶ [ğ“ y] g) â†” f â‰¤á¶ [ğ“ x] g :=
  eventually_eventually_nhds

/-- If two functions are equal in a neighbourhood of `x`, then for `y` sufficiently close
to `x` these functions are equal in a neighbourhood of `y`. -/
theorem Filter.EventuallyEq.eventuallyEq_nhds {f g : X â†’ Î±} (h : f =á¶ [ğ“ x] g) :
    âˆ€á¶  y in ğ“ x, f =á¶ [ğ“ y] g :=
  h.eventually_nhds

/-- If `f x â‰¤ g x` in a neighbourhood of `x`, then for `y` sufficiently close to `x` we have
`f x â‰¤ g x` in a neighbourhood of `y`. -/
theorem Filter.EventuallyLE.eventuallyLE_nhds [LE Î±] {f g : X â†’ Î±} (h : f â‰¤á¶ [ğ“ x] g) :
    âˆ€á¶  y in ğ“ x, f â‰¤á¶ [ğ“ y] g :=
  h.eventually_nhds

theorem all_mem_nhds (x : X) (P : Set X â†’ Prop) (hP : âˆ€ s t, s âŠ† t â†’ P s â†’ P t) :
    (âˆ€ s âˆˆ ğ“ x, P s) â†” âˆ€ s, IsOpen s â†’ x âˆˆ s â†’ P s :=
  ((nhds_basis_opens x).forall_iff hP).trans <| by simp only [@and_comm (x âˆˆ _), and_imp]

theorem all_mem_nhds_filter (x : X) (f : Set X â†’ Set Î±) (hf : âˆ€ s t, s âŠ† t â†’ f s âŠ† f t)
    (l : Filter Î±) : (âˆ€ s âˆˆ ğ“ x, f s âˆˆ l) â†” âˆ€ s, IsOpen s â†’ x âˆˆ s â†’ f s âˆˆ l :=
  all_mem_nhds _ _ fun s t ssubt h => mem_of_superset h (hf s t ssubt)

theorem tendsto_nhds {f : Î± â†’ X} {l : Filter Î±} :
    Tendsto f l (ğ“ x) â†” âˆ€ s, IsOpen s â†’ x âˆˆ s â†’ f â»Â¹' s âˆˆ l :=
  all_mem_nhds_filter _ _ (fun _ _ h => preimage_mono h) _

theorem tendsto_atTop_nhds [Nonempty Î±] [SemilatticeSup Î±] {f : Î± â†’ X} :
    Tendsto f atTop (ğ“ x) â†” âˆ€ U : Set X, x âˆˆ U â†’ IsOpen U â†’ âˆƒ N, âˆ€ n, N â‰¤ n â†’ f n âˆˆ U :=
  (atTop_basis.tendsto_iff (nhds_basis_opens x)).trans <| by
    simp only [and_imp, exists_prop, true_and, mem_Ici]

theorem tendsto_const_nhds {f : Filter Î±} : Tendsto (fun _ : Î± => x) f (ğ“ x) :=
  tendsto_nhds.mpr fun _ _ ha => univ_mem' fun _ => ha

theorem tendsto_atTop_of_eventually_const {Î¹ : Type*} [Preorder Î¹]
    {u : Î¹ â†’ X} {iâ‚€ : Î¹} (h : âˆ€ i â‰¥ iâ‚€, u i = x) : Tendsto u atTop (ğ“ x) :=
  Tendsto.congr' (EventuallyEq.symm ((eventually_ge_atTop iâ‚€).mono h)) tendsto_const_nhds

theorem tendsto_atBot_of_eventually_const {Î¹ : Type*} [Preorder Î¹]
    {u : Î¹ â†’ X} {iâ‚€ : Î¹} (h : âˆ€ i â‰¤ iâ‚€, u i = x) : Tendsto u atBot (ğ“ x) :=
  tendsto_atTop_of_eventually_const (Î¹ := Î¹áµ’áµˆ) h

theorem pure_le_nhds : pure â‰¤ (ğ“ : X â†’ Filter X) := fun _ _ hs => mem_pure.2 <| mem_of_mem_nhds hs

theorem tendsto_pure_nhds (f : Î± â†’ X) (a : Î±) : Tendsto f (pure a) (ğ“ (f a)) :=
  (tendsto_pure_pure f a).mono_right (pure_le_nhds _)

theorem OrderTop.tendsto_atTop_nhds [PartialOrder Î±] [OrderTop Î±] (f : Î± â†’ X) :
    Tendsto f atTop (ğ“ (f âŠ¤)) :=
  (tendsto_atTop_pure f).mono_right (pure_le_nhds _)

@[simp]
instance nhds_neBot : NeBot (ğ“ x) :=
  neBot_of_le (pure_le_nhds x)

theorem tendsto_nhds_of_eventually_eq {l : Filter Î±} {f : Î± â†’ X} (h : âˆ€á¶  x' in l, f x' = x) :
    Tendsto f l (ğ“ x) :=
  tendsto_const_nhds.congr' (.symm h)

theorem Filter.EventuallyEq.tendsto {l : Filter Î±} {f : Î± â†’ X} (hf : f =á¶ [l] fun _ â†¦ x) :
    Tendsto f l (ğ“ x) :=
  tendsto_nhds_of_eventually_eq hf

/-! ### Interior, closure and frontier in terms of neighborhoods -/

theorem interior_eq_nhds' : interior s = { x | s âˆˆ ğ“ x } :=
  Set.ext fun x => by simp only [mem_interior, mem_nhds_iff, mem_setOf_eq]

theorem interior_eq_nhds : interior s = { x | ğ“ x â‰¤ ğ“Ÿ s } :=
  interior_eq_nhds'.trans <| by simp only [le_principal_iff]

@[simp]
theorem interior_mem_nhds : interior s âˆˆ ğ“ x â†” s âˆˆ ğ“ x :=
  âŸ¨fun h => mem_of_superset h interior_subset, fun h =>
    IsOpen.mem_nhds isOpen_interior (mem_interior_iff_mem_nhds.2 h)âŸ©

theorem interior_setOf_eq {p : X â†’ Prop} : interior { x | p x } = { x | âˆ€á¶  y in ğ“ x, p y } :=
  interior_eq_nhds'

theorem isOpen_setOf_eventually_nhds {p : X â†’ Prop} : IsOpen { x | âˆ€á¶  y in ğ“ x, p y } := by
  simp only [â† interior_setOf_eq, isOpen_interior]

theorem subset_interior_iff_nhds {V : Set X} : s âŠ† interior V â†” âˆ€ x âˆˆ s, V âˆˆ ğ“ x := by
  simp_rw [subset_def, mem_interior_iff_mem_nhds]

theorem isOpen_iff_nhds : IsOpen s â†” âˆ€ x âˆˆ s, ğ“ x â‰¤ ğ“Ÿ s :=
  calc
    IsOpen s â†” s âŠ† interior s := subset_interior_iff_isOpen.symm
    _ â†” âˆ€ x âˆˆ s, ğ“ x â‰¤ ğ“Ÿ s := by simp_rw [interior_eq_nhds, subset_def, mem_setOf]

theorem TopologicalSpace.ext_iff_nhds {X} {t t' : TopologicalSpace X} :
    t = t' â†” âˆ€ x, @nhds _ t x = @nhds _ t' x :=
  âŸ¨fun H _ â†¦ congrFun (congrArg _ H) _, fun H â†¦ by ext; simp_rw [@isOpen_iff_nhds _ _ _, H]âŸ©

alias âŸ¨_, TopologicalSpace.ext_nhdsâŸ© := TopologicalSpace.ext_iff_nhds

theorem isOpen_iff_mem_nhds : IsOpen s â†” âˆ€ x âˆˆ s, s âˆˆ ğ“ x :=
  isOpen_iff_nhds.trans <| forall_congr' fun _ => imp_congr_right fun _ => le_principal_iff

/-- A set `s` is open iff for every point `x` in `s` and every `y` close to `x`, `y` is in `s`. -/
theorem isOpen_iff_eventually : IsOpen s â†” âˆ€ x, x âˆˆ s â†’ âˆ€á¶  y in ğ“ x, y âˆˆ s :=
  isOpen_iff_mem_nhds

theorem isOpen_singleton_iff_nhds_eq_pure (x : X) : IsOpen ({x} : Set X) â†” ğ“ x = pure x := by
  simp [â† (pure_le_nhds _).le_iff_eq, isOpen_iff_mem_nhds]

theorem isOpen_singleton_iff_punctured_nhds (x : X) : IsOpen ({x} : Set X) â†” ğ“[â‰ ] x = âŠ¥ := by
  rw [isOpen_singleton_iff_nhds_eq_pure, nhdsWithin, â† mem_iff_inf_principal_compl,
      le_antisymm_iff]
  simp [pure_le_nhds x]

theorem mem_closure_iff_frequently : x âˆˆ closure s â†” âˆƒá¶  x in ğ“ x, x âˆˆ s := by
  rw [Filter.Frequently, Filter.Eventually, â† mem_interior_iff_mem_nhds,
    closure_eq_compl_interior_compl, mem_compl_iff, compl_def]

alias âŸ¨_, Filter.Frequently.mem_closureâŸ© := mem_closure_iff_frequently

/-- A set `s` is closed iff for every point `x`, if there is a point `y` close to `x` that belongs
to `s` then `x` is in `s`. -/
theorem isClosed_iff_frequently : IsClosed s â†” âˆ€ x, (âˆƒá¶  y in ğ“ x, y âˆˆ s) â†’ x âˆˆ s := by
  rw [â† closure_subset_iff_isClosed]
  refine forall_congr' fun x => ?_
  rw [mem_closure_iff_frequently]

lemma nhdsWithin_neBot : (ğ“[s] x).NeBot â†” âˆ€ â¦ƒtâ¦„, t âˆˆ ğ“ x â†’ (t âˆ© s).Nonempty := by
  rw [nhdsWithin, inf_neBot_iff]
  exact forallâ‚‚_congr fun U _ â†¦
    âŸ¨fun h â†¦ h (mem_principal_self _), fun h u hsu â†¦ h.mono <| inter_subset_inter_right _ hsuâŸ©

@[gcongr]
theorem nhdsWithin_mono (x : X) {s t : Set X} (h : s âŠ† t) : ğ“[s] x â‰¤ ğ“[t] x :=
  inf_le_inf_left _ (principal_mono.mpr h)

theorem IsClosed.interior_union_left (_ : IsClosed s) :
    interior (s âˆª t) âŠ† s âˆª interior t := fun a âŸ¨u, âŸ¨âŸ¨huâ‚, huâ‚‚âŸ©, haâŸ©âŸ© =>
  (Classical.em (a âˆˆ s)).imp_right fun h =>
    mem_interior.mpr
      âŸ¨u âˆ© sá¶œ, fun _x hx => (huâ‚‚ hx.1).resolve_left hx.2, IsOpen.inter huâ‚ IsClosed.isOpen_compl,
        âŸ¨ha, hâŸ©âŸ©

theorem IsClosed.interior_union_right (h : IsClosed t) :
    interior (s âˆª t) âŠ† interior s âˆª t := by
  simpa only [union_comm _ t] using h.interior_union_left

theorem IsOpen.inter_closure (h : IsOpen s) : s âˆ© closure t âŠ† closure (s âˆ© t) :=
  compl_subset_compl.mp <| by
    simpa only [â† interior_compl, compl_inter] using IsClosed.interior_union_left h.isClosed_compl

theorem IsOpen.closure_inter (h : IsOpen t) : closure s âˆ© t âŠ† closure (s âˆ© t) := by
  simpa only [inter_comm t] using h.inter_closure

theorem Dense.open_subset_closure_inter (hs : Dense s) (ht : IsOpen t) :
    t âŠ† closure (t âˆ© s) :=
  calc
    t = t âˆ© closure s := by rw [hs.closure_eq, inter_univ]
    _ âŠ† closure (t âˆ© s) := ht.inter_closure

/-- The intersection of an open dense set with a dense set is a dense set. -/
theorem Dense.inter_of_isOpen_left (hs : Dense s) (ht : Dense t) (hso : IsOpen s) :
    Dense (s âˆ© t) := fun x =>
  closure_minimal hso.inter_closure isClosed_closure <| by simp [hs.closure_eq, ht.closure_eq]

/-- The intersection of a dense set with an open dense set is a dense set. -/
theorem Dense.inter_of_isOpen_right (hs : Dense s) (ht : Dense t) (hto : IsOpen t) :
    Dense (s âˆ© t) :=
  inter_comm t s â–¸ ht.inter_of_isOpen_left hs hto

theorem Dense.inter_nhds_nonempty (hs : Dense s) (ht : t âˆˆ ğ“ x) :
    (s âˆ© t).Nonempty :=
  let âŸ¨U, hsub, ho, hxâŸ© := mem_nhds_iff.1 ht
  (hs.inter_open_nonempty U ho âŸ¨x, hxâŸ©).mono fun _y hy => âŸ¨hy.2, hsub hy.1âŸ©

theorem closure_diff : closure s \ closure t âŠ† closure (s \ t) :=
  calc
    closure s \ closure t = (closure t)á¶œ âˆ© closure s := by simp only [diff_eq, inter_comm]
    _ âŠ† closure ((closure t)á¶œ âˆ© s) := (isOpen_compl_iff.mpr <| isClosed_closure).inter_closure
    _ = closure (s \ closure t) := by simp only [diff_eq, inter_comm]
    _ âŠ† closure (s \ t) := closure_mono <| diff_subset_diff (Subset.refl s) subset_closure

theorem Filter.Frequently.mem_of_closed (h : âˆƒá¶  x in ğ“ x, x âˆˆ s)
    (hs : IsClosed s) : x âˆˆ s :=
  hs.closure_subset h.mem_closure

theorem IsClosed.mem_of_frequently_of_tendsto {f : Î± â†’ X} {b : Filter Î±}
    (hs : IsClosed s) (h : âˆƒá¶  x in b, f x âˆˆ s) (hf : Tendsto f b (ğ“ x)) : x âˆˆ s :=
  (hf.frequently <| show âˆƒá¶  x in b, (fun y => y âˆˆ s) (f x) from h).mem_of_closed hs

theorem IsClosed.mem_of_tendsto {f : Î± â†’ X} {b : Filter Î±} [NeBot b]
    (hs : IsClosed s) (hf : Tendsto f b (ğ“ x)) (h : âˆ€á¶  x in b, f x âˆˆ s) : x âˆˆ s :=
  hs.mem_of_frequently_of_tendsto h.frequently hf

theorem mem_closure_of_frequently_of_tendsto {f : Î± â†’ X} {b : Filter Î±}
    (h : âˆƒá¶  x in b, f x âˆˆ s) (hf : Tendsto f b (ğ“ x)) : x âˆˆ closure s :=
  (hf.frequently h).mem_closure

theorem mem_closure_of_tendsto {f : Î± â†’ X} {b : Filter Î±} [NeBot b]
    (hf : Tendsto f b (ğ“ x)) (h : âˆ€á¶  x in b, f x âˆˆ s) : x âˆˆ closure s :=
  mem_closure_of_frequently_of_tendsto h.frequently hf

/-- Suppose that `f` sends the complement to `s` to a single point `x`, and `l` is some filter.
Then `f` tends to `x` along `l` restricted to `s` if and only if it tends to `x` along `l`. -/
theorem tendsto_inf_principal_nhds_iff_of_forall_eq {f : Î± â†’ X} {l : Filter Î±} {s : Set Î±}
    (h : âˆ€ a âˆ‰ s, f a = x) : Tendsto f (l âŠ“ ğ“Ÿ s) (ğ“ x) â†” Tendsto f l (ğ“ x) := by
  rw [tendsto_iff_comap, tendsto_iff_comap]
  replace h : ğ“Ÿ sá¶œ â‰¤ comap f (ğ“ x) := by
    rintro U âŸ¨t, ht, htUâŸ© x hx
    have : f x âˆˆ t := (h x hx).symm â–¸ mem_of_mem_nhds ht
    exact htU this
  refine âŸ¨fun h' => ?_, le_trans inf_le_leftâŸ©
  have := sup_le h' h
  rw [sup_inf_right, sup_principal, union_compl_self, principal_univ, inf_top_eq, sup_le_iff]
    at this
  exact this.1
