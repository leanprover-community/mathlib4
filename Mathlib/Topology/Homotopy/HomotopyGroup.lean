/-
Copyright (c) 2021 Roberto Alvarez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Roberto Alvarez
-/
import Mathlib.AlgebraicTopology.FundamentalGroupoid.FundamentalGroup
import Mathlib.GroupTheory.EckmannHilton
import Mathlib.Logic.Equiv.TransferInstance
import Mathlib.Algebra.Group.Ext

#align_import topology.homotopy.homotopy_group from "leanprover-community/mathlib"@"4c3e1721c58ef9087bbc2c8c38b540f70eda2e53"

/-!
# `n`th homotopy group

We define the `n`th homotopy group at `x : X`, `œÄ_n X x`, as the equivalence classes
of functions from the `n`-dimensional cube to the topological space `X`
that send the boundary to the base point `x`, up to homotopic equivalence.
Note that such functions are generalized loops `GenLoop (Fin n) x`; in particular
`GenLoop (Fin 1) x ‚âÉ Path x x`.

We show that `œÄ_0 X x` is equivalent to the path-connected components, and
that `œÄ_1 X x` is equivalent to the fundamental group at `x`.
We provide a group instance using path composition and show commutativity when `n > 1`.

## definitions

* `GenLoop N x` is the type of continuous functions `I^N ‚Üí X` that send the boundary to `x`,
* `HomotopyGroup.Pi n X x` denoted `œÄ_ n X x` is the quotient of `GenLoop (Fin n) x` by
  homotopy relative to the boundary,
* group instance `Group (œÄ_(n+1) X x)`,
* commutative group instance `CommGroup (œÄ_(n+2) X x)`.

TODO:
* `Œ©^M (Œ©^N X) ‚âÉ‚Çú Œ©^(M‚äïN) X`, and `Œ©^M X ‚âÉ‚Çú Œ©^N X` when `M ‚âÉ N`. Similarly for `œÄ_`.
* Path-induced homomorphisms. Show that `HomotopyGroup.pi1EquivFundamentalGroup`
  is a group isomorphism.
* Examples with `ùïä^n`: `œÄ_n (ùïä^n) = ‚Ñ§`, `œÄ_m (ùïä^n)` trivial for `m < n`.
* Actions of œÄ_1 on œÄ_n.
* Lie algebra: `‚ÅÖœÄ_(n+1), œÄ_(m+1)‚ÅÜ` contained in `œÄ_(n+m+1)`.

-/


open scoped unitInterval Topology

open Homeomorph

noncomputable section

scoped[Topology] notation "I^" N => N ‚Üí I

namespace Cube

/-- The points in a cube with at least one projection equal to 0 or 1. -/
def boundary (N : Type*) : Set (I^N) :=
  {y | ‚àÉ i, y i = 0 ‚à® y i = 1}
#align cube.boundary Cube.boundary

variable {N : Type*} [DecidableEq N]

/-- The forward direction of the homeomorphism
  between the cube $I^N$ and $I √ó I^{N\setminus\{j\}}$. -/
abbrev splitAt (i : N) : (I^N) ‚âÉ‚Çú I √ó I^{ j // j ‚â† i } :=
  funSplitAt I i
#align cube.split_at Cube.splitAt

/-- The backward direction of the homeomorphism
  between the cube $I^N$ and $I √ó I^{N\setminus\{j\}}$. -/
abbrev insertAt (i : N) : (I √ó I^{ j // j ‚â† i }) ‚âÉ‚Çú I^N :=
  (funSplitAt I i).symm
#align cube.insert_at Cube.insertAt

theorem insertAt_boundary (i : N) {t‚ÇÄ : I} {t}
    (H : (t‚ÇÄ = 0 ‚à® t‚ÇÄ = 1) ‚à® t ‚àà boundary { j // j ‚â† i }) : insertAt i ‚ü®t‚ÇÄ, t‚ü© ‚àà boundary N := by
  obtain H | ‚ü®j, H‚ü© := H
  ¬∑ use i; rwa [funSplitAt_symm_apply, dif_pos rfl]
  ¬∑ use j; rwa [funSplitAt_symm_apply, dif_neg j.prop, Subtype.coe_eta]
#align cube.insert_at_boundary Cube.insertAt_boundary

end Cube

variable (N X : Type*) [TopologicalSpace X] (x : X)

/-- The space of paths with both endpoints equal to a specified point `x : X`. -/
abbrev LoopSpace :=
  Path x x
#align loop_space LoopSpace

scoped[Topology.Homotopy] notation "Œ©" => LoopSpace

instance LoopSpace.inhabited : Inhabited (Path x x) :=
  ‚ü®Path.refl x‚ü©
#align loop_space.inhabited LoopSpace.inhabited

/-- The `n`-dimensional generalized loops based at `x` in a space `X` are
  continuous functions `I^n ‚Üí X` that sends the boundary to `x`.
  We allow an arbitrary indexing type `N` in place of `Fin n` here. -/
def GenLoop : Set C(I^N, X) :=
  {p | ‚àÄ y ‚àà Cube.boundary N, p y = x}
#align gen_loop GenLoop

@[inherit_doc] scoped[Topology.Homotopy] notation "Œ©^" => GenLoop

open Topology.Homotopy

variable {N X x}

namespace GenLoop

instance instFunLike : FunLike (Œ©^ N X x) (I^N) X where
  coe f := f.1
  coe_injective' := fun ‚ü®‚ü®f, _‚ü©, _‚ü© ‚ü®‚ü®g, _‚ü©, _‚ü© _ => by congr
#align gen_loop.fun_like GenLoop.instFunLike

@[ext]
theorem ext (f g : Œ©^ N X x) (H : ‚àÄ y, f y = g y) : f = g :=
  DFunLike.coe_injective' (funext H)
#align gen_loop.ext GenLoop.ext

@[simp]
theorem mk_apply (f : C(I^N, X)) (H y) : (‚ü®f, H‚ü© : Œ©^ N X x) y = f y :=
  rfl
#align gen_loop.mk_apply GenLoop.mk_apply

/-- Copy of a `GenLoop` with a new map from the unit cube equal to the old one.
  Useful to fix definitional equalities. -/
def copy (f : Œ©^ N X x) (g : (I^N) ‚Üí X) (h : g = f) : Œ©^ N X x :=
  ‚ü®‚ü®g, h.symm ‚ñ∏ f.1.2‚ü©, by convert f.2‚ü©
#align gen_loop.copy GenLoop.copy

/- porting note: this now requires the `instFunLike` instance,
  so the instance is now put before `copy`. -/
theorem coe_copy (f : Œ©^ N X x) {g : (I^N) ‚Üí X} (h : g = f) : ‚áë(copy f g h) = g :=
  rfl
#align gen_loop.coe_copy GenLoop.coe_copy

theorem copy_eq (f : Œ©^ N X x) {g : (I^N) ‚Üí X} (h : g = f) : copy f g h = f := by
  ext x
  exact congr_fun h x
#align gen_loop.copy_eq GenLoop.copy_eq

theorem boundary (f : Œ©^ N X x) : ‚àÄ y ‚àà Cube.boundary N, f y = x :=
  f.2
#align gen_loop.boundary GenLoop.boundary

/-- The constant `GenLoop` at `x`. -/
def const : Œ©^ N X x :=
  ‚ü®ContinuousMap.const _ x, fun _ _ => rfl‚ü©
#align gen_loop.const GenLoop.const

@[simp]
theorem const_apply {t} : (@const N X _ x) t = x :=
  rfl
#align gen_loop.const_apply GenLoop.const_apply

instance inhabited : Inhabited (Œ©^ N X x) :=
  ‚ü®const‚ü©

/-- The "homotopic relative to boundary" relation between `GenLoop`s. -/
def Homotopic (f g : Œ©^ N X x) : Prop :=
  f.1.HomotopicRel g.1 (Cube.boundary N)
#align gen_loop.homotopic GenLoop.Homotopic

namespace Homotopic

variable {f g h : Œ©^ N X x}

@[refl]
theorem refl (f : Œ©^ N X x) : Homotopic f f :=
  ContinuousMap.HomotopicRel.refl _
#align gen_loop.homotopic.refl GenLoop.Homotopic.refl

@[symm]
nonrec theorem symm (H : Homotopic f g) : Homotopic g f :=
  H.symm
#align gen_loop.homotopic.symm GenLoop.Homotopic.symm

@[trans]
nonrec theorem trans (H0 : Homotopic f g) (H1 : Homotopic g h) : Homotopic f h :=
  H0.trans H1
#align gen_loop.homotopic.trans GenLoop.Homotopic.trans

theorem equiv : Equivalence (@Homotopic N X _ x) :=
  ‚ü®Homotopic.refl, Homotopic.symm, Homotopic.trans‚ü©
#align gen_loop.homotopic.equiv GenLoop.Homotopic.equiv

instance setoid (N) (x : X) : Setoid (Œ©^ N X x) :=
  ‚ü®Homotopic, equiv‚ü©
#align gen_loop.homotopic.setoid GenLoop.Homotopic.setoid

end Homotopic

section LoopHomeo

variable [DecidableEq N]

/-- Loop from a generalized loop by currying $I^N ‚Üí X$ into $I ‚Üí (I^{N\setminus\{j\}} ‚Üí X)$. -/
@[simps]
def toLoop (i : N) (p : Œ©^ N X x) : Œ© (Œ©^ { j // j ‚â† i } X x) const where
  toFun t :=
    ‚ü®(p.val.comp (Cube.insertAt i).toContinuousMap).curry t, fun y yH =>
      p.property (Cube.insertAt i (t, y)) (Cube.insertAt_boundary i <| Or.inr yH)‚ü©
  source' := by ext t; refine p.property (Cube.insertAt i (0, t)) ‚ü®i, Or.inl ?_‚ü©; simp
  target' := by ext t; refine p.property (Cube.insertAt i (1, t)) ‚ü®i, Or.inr ?_‚ü©; simp
#align gen_loop.to_loop GenLoop.toLoop


theorem continuous_toLoop (i : N) : Continuous (@toLoop N X _ x _ i) :=
  Path.continuous_uncurry_iff.1 <|
    Continuous.subtype_mk
      (ContinuousMap.continuous_eval.comp <|
        Continuous.prod_map
          (ContinuousMap.continuous_curry.comp <|
            (ContinuousMap.continuous_comp_left _).comp continuous_subtype_val)
          continuous_id)
      _
#align gen_loop.continuous_to_loop GenLoop.continuous_toLoop

/-- Generalized loop from a loop by uncurrying $I ‚Üí (I^{N\setminus\{j\}} ‚Üí X)$ into $I^N ‚Üí X$. -/
@[simps]
def fromLoop (i : N) (p : Œ© (Œ©^ { j // j ‚â† i } X x) const) : Œ©^ N X x :=
  ‚ü®(ContinuousMap.comp ‚ü®Subtype.val, by fun_prop‚ü© p.toContinuousMap).uncurry.comp
    (Cube.splitAt i).toContinuousMap,
    by
    rintro y ‚ü®j, Hj‚ü©
    simp only [ContinuousMap.comp_apply, toContinuousMap_apply,
      funSplitAt_apply, ContinuousMap.uncurry_apply, ContinuousMap.coe_mk,
      Function.uncurry_apply_pair]
    obtain rfl | Hne := eq_or_ne j i
    ¬∑ cases' Hj with Hj Hj <;> simp only [Hj, p.coe_toContinuousMap, p.source, p.target] <;> rfl
    ¬∑ exact GenLoop.boundary _ _ ‚ü®‚ü®j, Hne‚ü©, Hj‚ü©‚ü©
#align gen_loop.from_loop GenLoop.fromLoop

theorem continuous_fromLoop (i : N) : Continuous (@fromLoop N X _ x _ i) :=
  ((ContinuousMap.continuous_comp_left _).comp <|
        ContinuousMap.continuous_uncurry.comp <|
          (ContinuousMap.continuous_comp _).comp continuous_induced_dom).subtype_mk
    _
#align gen_loop.continuous_from_loop GenLoop.continuous_fromLoop

theorem to_from (i : N) (p : Œ© (Œ©^ { j // j ‚â† i } X x) const) : toLoop i (fromLoop i p) = p := by
  simp_rw [toLoop, fromLoop, ContinuousMap.comp_assoc,
    toContinuousMap_comp_symm, ContinuousMap.comp_id]
  ext; rfl
#align gen_loop.to_from GenLoop.to_from

/-- The `n+1`-dimensional loops are in bijection with the loops in the space of
  `n`-dimensional loops with base point `const`.
  We allow an arbitrary indexing type `N` in place of `Fin n` here. -/
@[simps]
def loopHomeo (i : N) : Œ©^ N X x ‚âÉ‚Çú Œ© (Œ©^ { j // j ‚â† i } X x) const where
  toFun := toLoop i
  invFun := fromLoop i
  left_inv p := by ext; exact congr_arg p (by dsimp; exact Equiv.apply_symm_apply _ _)
  right_inv := to_from i
  continuous_toFun := continuous_toLoop i
  continuous_invFun := continuous_fromLoop i
#align gen_loop.loop_homeo GenLoop.loopHomeo

theorem toLoop_apply (i : N) {p : Œ©^ N X x} {t} {tn} :
    toLoop i p t tn = p (Cube.insertAt i ‚ü®t, tn‚ü©) :=
  rfl
#align gen_loop.to_loop_apply GenLoop.toLoop_apply

theorem fromLoop_apply (i : N) {p : Œ© (Œ©^ { j // j ‚â† i } X x) const} {t : I^N} :
    fromLoop i p t = p (t i) (Cube.splitAt i t).snd :=
  rfl
#align gen_loop.from_loop_apply GenLoop.fromLoop_apply

/-- Composition with `Cube.insertAt` as a continuous map. -/
abbrev cCompInsert (i : N) : C(C(I^N, X), C(I √ó I^{ j // j ‚â† i }, X)) :=
  ‚ü®fun f => f.comp (Cube.insertAt i).toContinuousMap,
    (Cube.insertAt i).toContinuousMap.continuous_comp_left‚ü©
#align gen_loop.c_comp_insert GenLoop.cCompInsert

/-- A homotopy between `n+1`-dimensional loops `p` and `q` constant on the boundary
  seen as a homotopy between two paths in the space of `n`-dimensional paths. -/
def homotopyTo (i : N) {p q : Œ©^ N X x} (H : p.1.HomotopyRel q.1 (Cube.boundary N)) :
    C(I √ó I, C(I^{ j // j ‚â† i }, X)) :=
  ((‚ü®_, ContinuousMap.continuous_curry‚ü© : C(_, _)).comp <|
      (cCompInsert i).comp H.toContinuousMap.curry).uncurry
#align gen_loop.homotopy_to GenLoop.homotopyTo

-- porting note (#11083): `@[simps]` no longer too slow in Lean 4 but does not generate this lemma.
theorem homotopyTo_apply (i : N) {p q : Œ©^ N X x} (H : p.1.HomotopyRel q.1 <| Cube.boundary N)
    (t : I √ó I) (t‚Çô : I^{ j // j ‚â† i }) :
    homotopyTo i H t t‚Çô = H (t.fst, Cube.insertAt i (t.snd, t‚Çô)) :=
  rfl
#align gen_loop.homotopy_to_apply GenLoop.homotopyTo_apply

theorem homotopicTo (i : N) {p q : Œ©^ N X x} :
    Homotopic p q ‚Üí (toLoop i p).Homotopic (toLoop i q) := by
  refine Nonempty.map fun H => ‚ü®‚ü®‚ü®fun t => ‚ü®homotopyTo i H t, ?_‚ü©, ?_‚ü©, ?_, ?_‚ü©, ?_‚ü©
  ¬∑ rintro y ‚ü®i, iH‚ü©
    rw [homotopyTo_apply, H.eq_fst, p.2]
    all_goals apply Cube.insertAt_boundary; right; exact ‚ü®i, iH‚ü©
  ¬∑ continuity
  iterate 2 intro; ext; erw [homotopyTo_apply, toLoop_apply]; swap
  ¬∑ apply H.apply_zero
  ¬∑ apply H.apply_one
  intro t y yH
  ext; erw [homotopyTo_apply]
  apply H.eq_fst; use i
  rw [funSplitAt_symm_apply, dif_pos rfl]; exact yH
#align gen_loop.homotopic_to GenLoop.homotopicTo

/-- The converse to `GenLoop.homotopyTo`: a homotopy between two loops in the space of
  `n`-dimensional loops can be seen as a homotopy between two `n+1`-dimensional paths. -/
@[simps!] def homotopyFrom (i : N) {p q : Œ©^ N X x} (H : (toLoop i p).Homotopy (toLoop i q)) :
    C(I √ó I^N, X) :=
  (ContinuousMap.comp ‚ü®_, ContinuousMap.continuous_uncurry‚ü©
          (ContinuousMap.comp ‚ü®Subtype.val, by continuity‚ü© H.toContinuousMap).curry).uncurry.comp <|
    (ContinuousMap.id I).prodMap (Cube.splitAt i).toContinuousMap
#align gen_loop.homotopy_from GenLoop.homotopyFrom
-- porting note (#11083): @[simps!] no longer too slow in Lean 4.
#align gen_loop.homotopy_from_apply GenLoop.homotopyFrom_apply

theorem homotopicFrom (i : N) {p q : Œ©^ N X x} :
    (toLoop i p).Homotopic (toLoop i q) ‚Üí Homotopic p q := by
  refine Nonempty.map fun H => ‚ü®‚ü®homotopyFrom i H, ?_, ?_‚ü©, ?_‚ü©
  pick_goal 3
  ¬∑ rintro t y ‚ü®j, jH‚ü©
    erw [homotopyFrom_apply]
    obtain rfl | h := eq_or_ne j i
    ¬∑ rw [H.eq_fst]; exacts [congr_arg p ((Cube.splitAt j).left_inv _), jH]
    ¬∑ rw [p.2 _ ‚ü®j, jH‚ü©]; apply boundary; exact ‚ü®‚ü®j, h‚ü©, jH‚ü©
  all_goals
    intro
    apply (homotopyFrom_apply _ _ _).trans
    first
    | rw [H.apply_zero]
    | rw [H.apply_one]
    first
    | apply congr_arg p
    | apply congr_arg q
    apply (Cube.splitAt i).left_inv
#align gen_loop.homotopic_from GenLoop.homotopicFrom

/-- Concatenation of two `GenLoop`s along the `i`th coordinate. -/
def transAt (i : N) (f g : Œ©^ N X x) : Œ©^ N X x :=
  copy (fromLoop i <| (toLoop i f).trans <| toLoop i g)
    (fun t => if (t i : ‚Ñù) ‚â§ 1 / 2
      then f (Function.update t i <| Set.projIcc 0 1 zero_le_one (2 * t i))
      else g (Function.update t i <| Set.projIcc 0 1 zero_le_one (2 * t i - 1)))
    (by
      ext1; symm
      dsimp only [Path.trans, fromLoop, Path.coe_mk_mk, Function.comp_apply, mk_apply,
        ContinuousMap.comp_apply, toContinuousMap_apply, funSplitAt_apply,
        ContinuousMap.uncurry_apply, ContinuousMap.coe_mk, Function.uncurry_apply_pair]
      split_ifs
      ¬∑ show f _ = _; congr 1
      ¬∑ show g _ = _; congr 1)
#align gen_loop.trans_at GenLoop.transAt

/-- Reversal of a `GenLoop` along the `i`th coordinate. -/
def symmAt (i : N) (f : Œ©^ N X x) : Œ©^ N X x :=
  (copy (fromLoop i (toLoop i f).symm) fun t => f fun j => if j = i then œÉ (t i) else t j) <| by
    ext1; change _ = f _; congr; ext1; simp
#align gen_loop.symm_at GenLoop.symmAt

theorem transAt_distrib {i j : N} (h : i ‚â† j) (a b c d : Œ©^ N X x) :
    transAt i (transAt j a b) (transAt j c d) = transAt j (transAt i a c) (transAt i b d) := by
  ext; simp_rw [transAt, coe_copy, Function.update_apply, if_neg h, if_neg h.symm]
  split_ifs <;>
    ¬∑ congr 1; ext1; simp only [Function.update, eq_rec_constant, dite_eq_ite]
      apply ite_ite_comm; rintro rfl; exact h.symm
#align gen_loop.trans_at_distrib GenLoop.transAt_distrib

theorem fromLoop_trans_toLoop {i : N} {p q : Œ©^ N X x} :
    fromLoop i ((toLoop i p).trans <| toLoop i q) = transAt i p q :=
  (copy_eq _ _).symm
#align gen_loop.from_loop_trans_to_loop GenLoop.fromLoop_trans_toLoop

theorem fromLoop_symm_toLoop {i : N} {p : Œ©^ N X x} : fromLoop i (toLoop i p).symm = symmAt i p :=
  (copy_eq _ _).symm
#align gen_loop.from_loop_symm_to_loop GenLoop.fromLoop_symm_toLoop

end LoopHomeo

end GenLoop

/-- The `n`th homotopy group at `x` defined as the quotient of `Œ©^n x` by the
  `GenLoop.Homotopic` relation. -/
def HomotopyGroup (N X : Type*) [TopologicalSpace X] (x : X) : Type _ :=
  Quotient (GenLoop.Homotopic.setoid N x)
#align homotopy_group HomotopyGroup

-- Porting note: in Lean 3 this instance was derived
instance : Inhabited (HomotopyGroup N X x) :=
  inferInstanceAs <| Inhabited <| Quotient (GenLoop.Homotopic.setoid N x)

variable [DecidableEq N]

open GenLoop

/-- Equivalence between the homotopy group of X and the fundamental group of
  `Œ©^{j // j ‚â† i} x`. -/
def homotopyGroupEquivFundamentalGroup (i : N) :
    HomotopyGroup N X x ‚âÉ FundamentalGroup (Œ©^ { j // j ‚â† i } X x) const := by
  refine Equiv.trans ?_ (CategoryTheory.Groupoid.isoEquivHom _ _).symm
  apply Quotient.congr (loopHomeo i).toEquiv
  exact fun p q => ‚ü®homotopicTo i, homotopicFrom i‚ü©
#align homotopy_group_equiv_fundamental_group homotopyGroupEquivFundamentalGroup

/-- Homotopy group of finite index. -/
abbrev HomotopyGroup.Pi (n) (X : Type*) [TopologicalSpace X] (x : X) :=
  HomotopyGroup (Fin n) _ x
#align homotopy_group.pi HomotopyGroup.Pi

scoped[Topology] notation "œÄ_" => HomotopyGroup.Pi

/-- The 0-dimensional generalized loops based at `x` are in bijection with `X`. -/
def genLoopHomeoOfIsEmpty (N x) [IsEmpty N] : Œ©^ N X x ‚âÉ‚Çú X where
  toFun f := f 0
  invFun y := ‚ü®ContinuousMap.const _ y, fun _ ‚ü®i, _‚ü© => isEmptyElim i‚ü©
  left_inv f := by ext; exact congr_arg f (Subsingleton.elim _ _)
  right_inv _ := rfl
  continuous_toFun := (ContinuousMap.continuous_eval_const (0 : N ‚Üí I)).comp continuous_induced_dom
  continuous_invFun := ContinuousMap.const'.2.subtype_mk _
#align gen_loop_homeo_of_is_empty genLoopHomeoOfIsEmpty

/-- The homotopy "group" indexed by an empty type is in bijection with
  the path components of `X`, aka the `ZerothHomotopy`. -/
def homotopyGroupEquivZerothHomotopyOfIsEmpty (N x) [IsEmpty N] :
    HomotopyGroup N X x ‚âÉ ZerothHomotopy X :=
  Quotient.congr (genLoopHomeoOfIsEmpty N x).toEquiv
    (by
      -- joined iff homotopic
      intros a‚ÇÅ a‚ÇÇ;
      constructor <;> rintro ‚ü®H‚ü©
      exacts
        [‚ü®{ toFun := fun t => H ‚ü®t, isEmptyElim‚ü©
            source' := (H.apply_zero _).trans (congr_arg a‚ÇÅ <| Subsingleton.elim _ _)
            target' := (H.apply_one _).trans (congr_arg a‚ÇÇ <| Subsingleton.elim _ _) }‚ü©,
        ‚ü®{  toFun := fun t0 => H t0.fst
            map_zero_left := fun _ => H.source.trans (congr_arg a‚ÇÅ <| Subsingleton.elim _ _)
            map_one_left := fun _ => H.target.trans (congr_arg a‚ÇÇ <| Subsingleton.elim _ _)
            prop' := fun _ _ ‚ü®i, _‚ü© => isEmptyElim i }‚ü©])
#align homotopy_group_equiv_zeroth_homotopy_of_is_empty homotopyGroupEquivZerothHomotopyOfIsEmpty

/-- The 0th homotopy "group" is in bijection with `ZerothHomotopy`. -/
def HomotopyGroup.pi0EquivZerothHomotopy : œÄ_ 0 X x ‚âÉ ZerothHomotopy X :=
  homotopyGroupEquivZerothHomotopyOfIsEmpty (Fin 0) x
#align homotopy_group.pi_0_equiv_zeroth_homotopy HomotopyGroup.pi0EquivZerothHomotopy

/-- The 1-dimensional generalized loops based at `x` are in bijection with loops at `x`. -/
def genLoopEquivOfUnique (N) [Unique N] : Œ©^ N X x ‚âÉ Œ© X x where
  toFun p :=
    Path.mk ‚ü®fun t => p fun _ => t, by continuity‚ü©
      (GenLoop.boundary _ (fun _ => 0) ‚ü®default, Or.inl rfl‚ü©)
      (GenLoop.boundary _ (fun _ => 1) ‚ü®default, Or.inr rfl‚ü©)
  invFun p :=
    ‚ü®‚ü®fun c => p (c default), by continuity‚ü©,
      by
      rintro y ‚ü®i, iH | iH‚ü© <;> cases Unique.eq_default i <;> apply (congr_arg p iH).trans
      exacts [p.source, p.target]‚ü©
  left_inv p := by ext y; exact congr_arg p (eq_const_of_unique y).symm
  right_inv p := by ext; rfl

#align gen_loop_equiv_of_unique genLoopEquivOfUnique

/- TODO (?): deducing this from `homotopyGroupEquivFundamentalGroup` would require
  combination of `CategoryTheory.Functor.mapAut` and
  `FundamentalGroupoid.fundamentalGroupoidFunctor` applied to `genLoopHomeoOfIsEmpty`,
  with possibly worse defeq. -/
/-- The homotopy group at `x` indexed by a singleton is in bijection with the fundamental group,
  i.e. the loops based at `x` up to homotopy. -/
def homotopyGroupEquivFundamentalGroupOfUnique (N) [Unique N] :
    HomotopyGroup N X x ‚âÉ FundamentalGroup X x := by
  refine Equiv.trans ?_ (CategoryTheory.Groupoid.isoEquivHom _ _).symm
  refine Quotient.congr (genLoopEquivOfUnique N) ?_
  intros a‚ÇÅ a‚ÇÇ; constructor <;> rintro ‚ü®H‚ü©
  ¬∑ exact
      ‚ü®{  toFun := fun tx => H (tx.fst, fun _ => tx.snd)
          map_zero_left := fun _ => H.apply_zero _
          map_one_left := fun _ => H.apply_one _
          prop' := fun t y iH => H.prop' _ _ ‚ü®default, iH‚ü© }‚ü©
  refine
    ‚ü®‚ü®‚ü®‚ü®fun tx => H (tx.fst, tx.snd default), H.continuous.comp ?_‚ü©, fun y => ?_, fun y => ?_‚ü©, ?_‚ü©‚ü©
  ¬∑ exact continuous_fst.prod_mk ((continuous_apply _).comp continuous_snd)
  ¬∑ exact (H.apply_zero _).trans (congr_arg a‚ÇÅ (eq_const_of_unique y).symm)
  ¬∑ exact (H.apply_one _).trans (congr_arg a‚ÇÇ (eq_const_of_unique y).symm)
  ¬∑ rintro t y ‚ü®i, iH‚ü©
    cases Unique.eq_default i
    exact (H.eq_fst _ iH).trans (congr_arg a‚ÇÅ (eq_const_of_unique y).symm)
#align homotopy_group_equiv_fundamental_group_of_unique homotopyGroupEquivFundamentalGroupOfUnique

/-- The first homotopy group at `x` is in bijection with the fundamental group. -/
def HomotopyGroup.pi1EquivFundamentalGroup : œÄ_ 1 X x ‚âÉ FundamentalGroup X x :=
  homotopyGroupEquivFundamentalGroupOfUnique (Fin 1)
#align homotopy_group.pi_1_equiv_fundamental_group HomotopyGroup.pi1EquivFundamentalGroup

namespace HomotopyGroup

/-- Group structure on `HomotopyGroup N X x` for nonempty `N` (in particular `œÄ_(n+1) X x`). -/
instance group (N) [DecidableEq N] [Nonempty N] : Group (HomotopyGroup N X x) :=
  (homotopyGroupEquivFundamentalGroup <| Classical.arbitrary N).group
#align homotopy_group.group HomotopyGroup.group

/-- Group structure on `HomotopyGroup` obtained by pulling back path composition along the
  `i`th direction. The group structures for two different `i j : N` distribute over each
  other, and therefore are equal by the Eckmann-Hilton argument. -/
abbrev auxGroup (i : N) : Group (HomotopyGroup N X x) :=
  (homotopyGroupEquivFundamentalGroup i).group
#align homotopy_group.aux_group HomotopyGroup.auxGroup

theorem isUnital_auxGroup (i : N) :
    EckmannHilton.IsUnital (auxGroup i).mul (‚ü¶const‚üß : HomotopyGroup N X x) where
  left_id := (auxGroup i).one_mul
  right_id := (auxGroup i).mul_one
#align homotopy_group.is_unital_aux_group HomotopyGroup.isUnital_auxGroup

theorem auxGroup_indep (i j : N) : (auxGroup i : Group (HomotopyGroup N X x)) = auxGroup j := by
  by_cases h : i = j; ¬∑ rw [h]
  refine Group.ext (EckmannHilton.mul (isUnital_auxGroup i) (isUnital_auxGroup j) ?_)
  rintro ‚ü®a‚ü© ‚ü®b‚ü© ‚ü®c‚ü© ‚ü®d‚ü©
  change Quotient.mk' _ = _
  apply congr_arg Quotient.mk'
  simp only [fromLoop_trans_toLoop, transAt_distrib h, coe_toEquiv, loopHomeo_apply,
    coe_symm_toEquiv, loopHomeo_symm_apply]
#align homotopy_group.aux_group_indep HomotopyGroup.auxGroup_indep

theorem transAt_indep {i} (j) (f g : Œ©^ N X x) :
    (‚ü¶transAt i f g‚üß : HomotopyGroup N X x) = ‚ü¶transAt j f g‚üß := by
  simp_rw [‚Üê fromLoop_trans_toLoop]
  let m := fun (G) (_ : Group G) => ((¬∑ * ¬∑) : G ‚Üí G ‚Üí G)
  exact congr_fun‚ÇÇ (congr_arg (m <| HomotopyGroup N X x) <| auxGroup_indep i j) ‚ü¶g‚üß ‚ü¶f‚üß
#align homotopy_group.trans_at_indep HomotopyGroup.transAt_indep

theorem symmAt_indep {i} (j) (f : Œ©^ N X x) :
    (‚ü¶symmAt i f‚üß : HomotopyGroup N X x) = ‚ü¶symmAt j f‚üß := by
  simp_rw [‚Üê fromLoop_symm_toLoop]
  let inv := fun (G) (_ : Group G) => ((¬∑‚Åª¬π) : G ‚Üí G)
  exact congr_fun (congr_arg (inv <| HomotopyGroup N X x) <| auxGroup_indep i j) ‚ü¶f‚üß
#align homotopy_group.symm_at_indep HomotopyGroup.symmAt_indep

/-- Characterization of multiplicative identity -/
theorem one_def [Nonempty N] : (1 : HomotopyGroup N X x) = ‚ü¶const‚üß :=
  rfl
#align homotopy_group.one_def HomotopyGroup.one_def

/-- Characterization of multiplication -/
theorem mul_spec [Nonempty N] {i} {p q : Œ©^ N X x} :
    -- Porting note (#11215): TODO: introduce `HomotopyGroup.mk` and remove defeq abuse.
    ((¬∑ * ¬∑) : _ ‚Üí _ ‚Üí HomotopyGroup N X x) ‚ü¶p‚üß ‚ü¶q‚üß = ‚ü¶transAt i q p‚üß := by
  rw [transAt_indep (Classical.arbitrary N) q, ‚Üê fromLoop_trans_toLoop]
  apply Quotient.sound
  rfl
#align homotopy_group.mul_spec HomotopyGroup.mul_spec

/-- Characterization of multiplicative inverse -/
theorem inv_spec [Nonempty N] {i} {p : Œ©^ N X x} :
    ((‚ü¶p‚üß)‚Åª¬π : HomotopyGroup N X x) = ‚ü¶symmAt i p‚üß := by
  rw [symmAt_indep (Classical.arbitrary N) p, ‚Üê fromLoop_symm_toLoop]
  apply Quotient.sound
  rfl
#align homotopy_group.inv_spec HomotopyGroup.inv_spec

/-- Multiplication on `HomotopyGroup N X x` is commutative for nontrivial `N`.
  In particular, multiplication on `œÄ_(n+2)` is commutative. -/
instance commGroup [Nontrivial N] : CommGroup (HomotopyGroup N X x) :=
  let h := exists_ne (Classical.arbitrary N)
  @EckmannHilton.commGroup (HomotopyGroup N X x) _ 1 (isUnital_auxGroup <| Classical.choose h) _
    (by
      rintro ‚ü®a‚ü© ‚ü®b‚ü© ‚ü®c‚ü© ‚ü®d‚ü©
      apply congr_arg Quotient.mk'
      simp only [fromLoop_trans_toLoop, transAt_distrib <| Classical.choose_spec h, coe_toEquiv,
        loopHomeo_apply, coe_symm_toEquiv, loopHomeo_symm_apply])
#align homotopy_group.comm_group HomotopyGroup.commGroup

end HomotopyGroup
