/-
Copyright (c) 2025 Junyan Xu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Junyan Xu
-/
module

public import Mathlib.AlgebraicTopology.FundamentalGroupoid.FundamentalGroup
public import Mathlib.AlgebraicTopology.FundamentalGroupoid.SimplyConnected
public import Mathlib.Topology.Connected.LocPathConnected
public import Mathlib.Topology.Covering.Basic
public import Mathlib.Topology.Homotopy.Path
public import Mathlib.Topology.UnitInterval

/-!
# The homotopy lifting property for covering maps

- `IsCoveringMap.exists_path_lifts`, `IsCoveringMap.liftPath`: any path in the base of a covering
  map lifts uniquely to the covering space (given a lift of the starting point).

- `IsCoveringMap.liftHomotopy`: any homotopy `I Ã— A â†’ X` in the base of a covering map `E â†’ X` can
  be lifted to a homotopy `I Ã— A â†’ E`, starting from a given lift of the restriction `{0} Ã— A â†’ X`.

- `IsCoveringMap.existsUnique_continuousMap_lifts`: any continuous map from a simply-connected,
  locally path-connected space lifts uniquely through a covering map (given a lift of an
  arbitrary point).
-/

@[expose] public section

open Topology unitInterval

variable {E X A : Type*} [TopologicalSpace E] [TopologicalSpace X] [TopologicalSpace A] {p : E â†’ X}

namespace IsLocalHomeomorph

variable (homeo : IsLocalHomeomorph p)
include homeo

/-- If `p : E â†’ X` is a local homeomorphism, and if `g : I Ã— A â†’ E` is a lift of `f : C(I Ã— A, X)`
  continuous on `{0} Ã— A âˆª I Ã— {a}` for some `a : A`, then there exists a neighborhood `N âˆˆ ğ“ a`
  and `g' : I Ã— A â†’ E` continuous on `I Ã— N` that agrees with `g` on `{0} Ã— A âˆª I Ã— {a}`.
  The proof follows [hatcher02], Proof of Theorem 1.7, p.30.

  Possible TODO: replace `I` by an arbitrary space assuming `A` is locally connected
  and `p` is a separated map, which guarantees uniqueness and therefore well-definedness
  on the intersections. -/
theorem exists_lift_nhds {f : C(I Ã— A, X)} {g : I Ã— A â†’ E} (g_lifts : p âˆ˜ g = f)
    (cont_0 : Continuous (g âŸ¨0, Â·âŸ©)) (a : A) (cont_a : Continuous (g âŸ¨Â·, aâŸ©)) :
    âˆƒ N âˆˆ ğ“ a, âˆƒ g' : I Ã— A â†’ E, ContinuousOn g' (Set.univ Ã—Ë¢ N) âˆ§ p âˆ˜ g' = f âˆ§
      (âˆ€ a, g' (0, a) = g (0, a)) âˆ§ âˆ€ t, g' (t, a) = g (t, a) := by
  -- For every `e : E`, upgrade `p` to a LocalHomeomorph `q e` around `e`.
  choose q mem_source hpq using homeo
  /- Using the hypothesis `cont_a`, we partition the unit interval so that for each
    subinterval `[tâ‚™, tâ‚™â‚Šâ‚]`, the image `g ([tâ‚™, tâ‚™â‚Šâ‚] Ã— {a})` is contained in the
    domain of some local homeomorphism `q e`. -/
  obtain âŸ¨t, t_0, t_mono, âŸ¨n_max, h_maxâŸ©, t_subâŸ© :=
    exists_monotone_Icc_subset_open_cover_unitInterval
      (fun e â†¦ (q e).open_source.preimage cont_a)
      fun t _ â†¦ Set.mem_iUnion.mpr âŸ¨g (t, a), mem_source _âŸ©
  /- We aim to inductively prove the existence of Nâ‚™ and g' continuous on [0, tâ‚™] Ã— Nâ‚™ for each n,
    and get the desired result by taking some n with tâ‚™ = 1. -/
  suffices âˆ€ n, âˆƒ N, a âˆˆ N âˆ§ IsOpen N âˆ§ âˆƒ g' : I Ã— A â†’ E, ContinuousOn g' (Set.Icc 0 (t n) Ã—Ë¢ N) âˆ§
      p âˆ˜ g' = f âˆ§ (âˆ€ a, g' (0, a) = g (0, a)) âˆ§ âˆ€ t' â‰¤ t n, g' (t', a) = g (t', a) by
    obtain âŸ¨N, haN, N_open, hNâŸ© := this n_max
    simp_rw [h_max _ le_rfl] at hN
    refine âŸ¨N, N_open.mem_nhds haN, ?_âŸ©; convert hN
    Â· rw [eq_comm, Set.eq_univ_iff_forall]; exact fun t â†¦ âŸ¨bot_le, le_topâŸ©
    Â· rw [imp_iff_right]; exact le_top
  refine Nat.rec âŸ¨_, Set.mem_univ a, isOpen_univ, g, ?_, g_lifts, fun a â†¦ rfl, fun _ _ â†¦ rflâŸ©
    (fun n âŸ¨N, haN, N_open, g', cont_g', g'_lifts, g'_0, g'_aâŸ© â†¦ ?_)
  Â· -- the n = 0 case is covered by the hypothesis cont_0.
    refine (cont_0.comp continuous_snd).continuousOn.congr (fun ta âŸ¨ht, _âŸ© â†¦ ?_)
    rw [t_0, Set.Icc_self, Set.mem_singleton_iff] at ht; rw [â† ta.eta, ht]; rfl
  /- Since g ([tâ‚™, tâ‚™â‚Šâ‚] Ã— {a}) is contained in the domain of some local homeomorphism `q e` and
    g lifts f, f ([tâ‚™, tâ‚™â‚Šâ‚] Ã— {a}) is contained in the codomain (`target`) of `q e`. -/
  obtain âŸ¨e, h_subâŸ© := t_sub n
  have : Set.Icc (t n) (t (n + 1)) Ã—Ë¢ {a} âŠ† f â»Â¹' (q e).target := by
    rintro âŸ¨t0, a'âŸ© âŸ¨ht, haâŸ©
    rw [Set.mem_singleton_iff] at ha; dsimp only at ha
    rw [â† g_lifts, hpq e, ha]
    exact (q e).map_source (h_sub ht)
  /- Using compactness of [tâ‚™, tâ‚™â‚Šâ‚], we can find a neighborhood v of a such that
    f ([tâ‚™, tâ‚™â‚Šâ‚] Ã— v) is contained in the codomain of `q e`. -/
  obtain âŸ¨u, v, -, v_open, hu, hav, huvâŸ© := generalized_tube_lemma isClosed_Icc.isCompact
    isCompact_singleton ((q e).open_target.preimage f.continuous) this
  classical
  /- Use the inverse of `q e` to extend g' from [0, tâ‚™] Ã— Nâ‚™â‚Šâ‚ to [0, tâ‚™â‚Šâ‚] Ã— Nâ‚™â‚Šâ‚, where
    Nâ‚™â‚Šâ‚ âŠ† v âˆ© Nâ‚™ is such that {tâ‚™} Ã— Nâ‚™â‚Šâ‚ is mapped to the domain (`source`) of `q e` by `g'`. -/
  refine âŸ¨_, ?_, v_open.inter <| (cont_g'.comp (Continuous.prodMk_right <| t n).continuousOn
      fun a ha â†¦ âŸ¨?_, haâŸ©).isOpen_inter_preimage N_open (q e).open_source,
    fun ta â†¦ if ta.1 â‰¤ t n then g' ta else if f ta âˆˆ (q e).target then (q e).symm (f ta) else g ta,
    .if (fun ta âŸ¨âŸ¨_, hav, _, haâŸ©, hfrâŸ© â†¦ ?_) (cont_g'.mono fun ta âŸ¨hta, htâŸ© â†¦ ?_) ?_,
    ?_, fun a â†¦ ?_, fun t0 htn1 â†¦ ?_âŸ©
  Â· refine âŸ¨Set.singleton_subset_iff.mp hav, haN, ?_âŸ©
    change g' (t n, a) âˆˆ (q e).source; rw [g'_a _ le_rfl]
    exact h_sub âŸ¨le_rfl, t_mono n.le_succâŸ©
  Â· rw [â† t_0]; exact âŸ¨t_mono n.zero_le, le_rflâŸ©
  Â· have ht := Set.mem_setOf.mp (frontier_le_subset_eq continuous_fst continuous_const hfr)
    have : f ta âˆˆ (q e).target := huv âŸ¨hu (by rw [ht]; exact âŸ¨le_rfl, t_mono n.le_succâŸ©), havâŸ©
    rw [if_pos this]
    -- here we use that {tâ‚™} Ã— Nâ‚™â‚Šâ‚ is mapped to the domain of `q e`
    apply (q e).injOn (by rwa [â† ta.eta, ht]) ((q e).map_target this)
    rw [(q e).right_inv this, â† hpq e]; exact congr_fun g'_lifts ta
  Â· rw [closure_le_eq continuous_fst continuous_const] at ht
    exact âŸ¨âŸ¨hta.1.1, htâŸ©, hta.2.2.1âŸ©
  Â· simp_rw [not_le]; exact (ContinuousOn.congr ((q e).continuousOn_invFun.comp f.2.continuousOn
      fun _ h â†¦ huv âŸ¨hu âŸ¨h.2, h.1.1.2âŸ©, h.1.2.1âŸ©)
      fun _ h â†¦ if_pos <| huv âŸ¨hu âŸ¨h.2, h.1.1.2âŸ©, h.1.2.1âŸ©).mono
        (Set.inter_subset_inter_right _ <| closure_lt_subset_le continuous_const continuous_fst)
  Â· ext ta; rw [Function.comp_apply]; split_ifs with _ hv
    Â· exact congr_fun g'_lifts ta
    Â· rw [hpq e, (q e).right_inv hv]
    Â· exact congr_fun g_lifts ta
  Â· rw [â† g'_0]; exact if_pos bot_le
  Â· dsimp only; split_ifs with htn hf
    Â· exact g'_a t0 htn
    Â· apply (q e).injOn ((q e).map_target hf) (h_sub âŸ¨le_of_not_ge htn, htn1âŸ©)
      rw [(q e).right_inv hf, â† hpq e]; exact (congr_fun g_lifts _).symm
    Â· rfl

variable (sep : IsSeparatedMap p)
include sep

theorem continuous_lift (f : C(I Ã— A, X)) {g : I Ã— A â†’ E} (g_lifts : p âˆ˜ g = f)
    (cont_0 : Continuous (g âŸ¨0, Â·âŸ©)) (cont_A : âˆ€ a, Continuous (g âŸ¨Â·, aâŸ©)) : Continuous g := by
  rw [continuous_iff_continuousAt]
  intro âŸ¨t, aâŸ©
  obtain âŸ¨N, haN, g', cont_g', g'_lifts, g'_0, -âŸ© :=
    homeo.exists_lift_nhds g_lifts cont_0 a (cont_A a)
  refine (cont_g'.congr fun âŸ¨t, aâŸ© âŸ¨_, haâŸ© â†¦ ?_).continuousAt (prod_mem_nhds Filter.univ_mem haN)
  refine congr_fun (sep.eq_of_comp_eq homeo.isLocallyInjective (cont_A a)
    (cont_g'.comp_continuous (.prodMk_left a) fun _ â†¦ âŸ¨âŸ¨âŸ©, haâŸ©) ?_ 0 (g'_0 a).symm) t
  ext t; apply congr_fun (g_lifts.trans g'_lifts.symm)

/-- The abstract monodromy theorem: if `Î³â‚€` and `Î³â‚` are two paths in a topological space `X`,
  `Î³` is a homotopy between them relative to the endpoints, and the path at each time step of
  the homotopy, `Î³ (t, Â·)`, lifts to a continuous path `Î“ t` through a separated local
  homeomorphism `p : E â†’ X`, starting from some point in `E` independent of `t`. Then the
  endpoints of these lifts are also independent of `t`.

  This can be applied to continuation of analytic functions as follows: for a sheaf of analytic
  functions on an analytic manifold `X`, we may consider its Ã©tale space `E` (whose points are
  analytic germs) with the natural projection `p : E â†’ X`, which is a local homeomorphism and a
  separated map (because two analytic functions agreeing on a nonempty open set agree on the
  whole connected component). An analytic continuation of a germ along a path `Î³ (t, Â·) : C(I, X)`
  corresponds to a continuous lift of `Î³ (t, Â·)` to `E` starting from that germ. If `Î³` is a
  homotopy and the germ admits continuation along every path `Î³ (t, Â·)`, then the result of the
  continuations are independent of `t`. In particular, if `X` is simply connected and an analytic
  germ at `p : X` admits a continuation along every path in `X` from `p` to `q : X`, then the
  continuation to `q` is independent of the path chosen. -/
theorem monodromy_theorem {Î³â‚€ Î³â‚ : C(I, X)} (Î³ : Î³â‚€.HomotopyRel Î³â‚ {0,1}) (Î“ : I â†’ C(I, E))
    (Î“_lifts : âˆ€ t s, p (Î“ t s) = Î³ (t, s)) (Î“_0 : âˆ€ t, Î“ t 0 = Î“ 0 0) (t : I) :
    Î“ t 1 = Î“ 0 1 := by
  have := homeo.continuous_lift sep (.comp Î³ .prodSwap) (g := fun st â†¦ Î“ st.2 st.1) ?_ ?_ ?_
  Â· apply sep.const_of_comp homeo.isLocallyInjective (this.comp (.prodMk_right 1))
    intro t t'; change p (Î“ _ _) = p (Î“ _ _); simp_rw [Î“_lifts, Î³.eq_fst _ (.inr rfl)]
  Â· ext; apply Î“_lifts
  Â· simp_rw [Î“_0]; exact continuous_const
  Â· exact fun t â†¦ (Î“ t).2

omit sep
open PathConnectedSpace (somePath) in
/-- A map `f` from a path-connected, locally path-connected space `A` to another space `X` lifts
  uniquely through a local homeomorphism `p : E â†’ X` if for every path `Î³` in `A`, the composed
  path `f âˆ˜ Î³` in `X` lifts to `E` with endpoint only dependent on the endpoint of `Î³` and
  independent of the path chosen. In this theorem, we require that a specific point `aâ‚€ : A` is
  lifted to a specific point `eâ‚€ : E` over `aâ‚€`. -/
theorem existsUnique_continuousMap_lifts [PathConnectedSpace A] [LocPathConnectedSpace A]
    (f : C(A, X)) (aâ‚€ : A) (eâ‚€ : E) (he : p eâ‚€ = f aâ‚€)
    (ex : âˆ€ Î³ : C(I, A), Î³ 0 = aâ‚€ â†’ âˆƒ Î“ : C(I, E), Î“ 0 = eâ‚€ âˆ§ p âˆ˜ Î“ = f.comp Î³)
    (uniq : âˆ€ Î³ Î³' : C(I, A), âˆ€ Î“ Î“' : C(I, E), Î³ 0 = aâ‚€ â†’ Î³' 0 = aâ‚€ â†’ Î“ 0 = eâ‚€ â†’ Î“' 0 = eâ‚€ â†’
      p âˆ˜ Î“ = f.comp Î³ â†’ p âˆ˜ Î“' = f.comp Î³' â†’ Î³ 1 = Î³' 1 â†’ Î“ 1 = Î“' 1) :
    âˆƒ! F : C(A, E), F aâ‚€ = eâ‚€ âˆ§ p âˆ˜ F = f := by
  choose Î“ Î“_0 Î“_lifts using ex
  let F (a : A) : E := Î“ _ (somePath aâ‚€ a).source 1
  have (a : A) : p (F a) = f a := by simpa using congr_fun (Î“_lifts _ (Path.source _)) 1
  refine âŸ¨âŸ¨F, continuous_iff_continuousAt.mpr fun a â†¦ ?_âŸ©, âŸ¨?_, funext thisâŸ©, fun F' âŸ¨F'_0, hpF'âŸ© â†¦
    DFunLike.ext _ _ fun a â†¦ ?_âŸ©
  Â· obtain âŸ¨p, hep, rflâŸ© := homeo (F a)
    have hfap : f a âˆˆ p.target := by rw [â† this]; exact p.map_source hep
    refine ContinuousAt.congr (f := p.symm âˆ˜ f) ((p.continuousOn_symm.continuousAt <|
      p.open_target.mem_nhds hfap).comp f.2.continuousAt) ?_
    have âŸ¨U, âŸ¨haU, U_connâŸ©, hUpâŸ© := (path_connected_basis a).mem_iff.mp
      ((p.open_target.preimage f.continuous).mem_nhds hfap)
    refine Filter.mem_of_superset haU fun x hxU â†¦ ?_
    have âŸ¨Î³, hÎ³âŸ© := U_conn.joinedIn _ (mem_of_mem_nhds haU) _ hxU
    let Î“' : Path eâ‚€ ((p.symm âˆ˜ f) a) :=
      âŸ¨Î“ _ (somePath aâ‚€ a).source, Î“_0 .., by simp [â† this, hep, F]âŸ©
    specialize uniq ((somePath aâ‚€ a).trans Î³) _ (Î“'.trans <| Î³.map' <| p.continuousOn_symm.comp
      f.2.continuousOn <| by rintro _ âŸ¨t, rflâŸ©; exact hUp (hÎ³ _)) _ (by simp) (somePath aâ‚€ x).source
      (by simp) (Î“_0 _ (somePath aâ‚€ x).source) _ (Î“_lifts ..) (by simp)
    Â· ext
      simp only [Function.comp, ContinuousMap.coe_coe, Path.trans_apply, ContinuousMap.coe_comp]
      split_ifs
      Â· apply congr_fun (Î“_lifts ..)
      Â· simp [Path.map', p.right_inv (hUp (hÎ³ _))]
    simpa using uniq
  Â· exact uniq _ (.const I aâ‚€) _ (.const I eâ‚€) (somePath aâ‚€ aâ‚€).source rfl (Î“_0 ..) rfl (Î“_lifts ..)
      (by simpa) (Path.target _)
  Â· let Î³ := somePath aâ‚€ a
    simpa using uniq _ _ (F'.comp Î³) (Î“ _ Î³.source) Î³.source Î³.source (by simpa) (Î“_0 ..)
      (by simp [â† Function.comp_assoc, hpF']) (Î“_lifts ..) rfl

end IsLocalHomeomorph

namespace IsCoveringMap
variable (cov : IsCoveringMap p)
include cov

section path_lifting
variable (Î³ : C(I, X)) (e : E) (Î³_0 : Î³ 0 = p e)
include Î³_0

/-- The path lifting property (existence) for covering maps. -/
theorem exists_path_lifts : âˆƒ Î“ : C(I, E), p âˆ˜ Î“ = Î³ âˆ§ Î“ 0 = e := by
  let U x := (cov x).2.choose
  choose mem_base U_open _ H _ using fun x â†¦ (cov x).2.choose_spec
  obtain âŸ¨t, t_0, t_mono, âŸ¨n_max, h_maxâŸ©, t_subâŸ© :=
    exists_monotone_Icc_subset_open_cover_unitInterval
    (fun x â†¦ (U_open x).preimage Î³.continuous) fun t _ â†¦ Set.mem_iUnion.2 âŸ¨Î³ t, mem_base _âŸ©
  suffices âˆ€ n, âˆƒ Î“ : I â†’ E, ContinuousOn Î“ (Set.Icc 0 (t n)) âˆ§
      (Set.Icc 0 (t n)).EqOn (p âˆ˜ Î“) Î³ âˆ§ Î“ 0 = e by
    obtain âŸ¨Î“, cont, eqOn, Î“_0âŸ© := this n_max
    rw [h_max _ le_rfl] at cont eqOn
    exact âŸ¨âŸ¨Î“, continuousOn_univ.mp
      (by convert cont; rw [eq_comm, Set.eq_univ_iff_forall]; exact fun t â†¦ âŸ¨bot_le, le_topâŸ©)âŸ©,
      funext fun _ â†¦ eqOn âŸ¨bot_le, le_topâŸ©, Î“_0âŸ©
  intro n
  induction n with
  | zero =>
    refine âŸ¨fun _ â†¦ e, continuous_const.continuousOn, fun t ht â†¦ ?_, rflâŸ©
    rw [t_0, Set.Icc_self, Set.mem_singleton_iff] at ht; subst ht; exact Î³_0.symm
  | succ n ih => ?_
  obtain âŸ¨Î“, cont, eqOn, Î“_0âŸ© := ih
  obtain âŸ¨x, t_subâŸ© := t_sub n
  have pÎ“tn : p (Î“ (t n)) = Î³ (t n) := eqOn âŸ¨t_0 â–¸ t_mono n.zero_le, le_rflâŸ©
  have : Nonempty (p â»Â¹' {x}) :=
    âŸ¨(H x âŸ¨Î“ (t n), Set.mem_preimage.mpr (pÎ“tn â–¸ t_sub âŸ¨le_rfl, t_mono n.le_succâŸ©)âŸ©).2âŸ©
  let q := (cov x).toTrivialization
  refine âŸ¨fun s â†¦ if s â‰¤ t n then Î“ s else q.invFun (Î³ s, (q (Î“ (t n))).2),
    .if (fun s hs â†¦ ?_) (cont.mono fun _ h â†¦ ?_) ?_, fun s hs â†¦ ?_, ?_âŸ©
  Â· cases frontier_Iic_subset _ hs.2
    rw [â† pÎ“tn]
    refine (q.symm_apply_mk_proj ?_).symm
    rw [q.mem_source, pÎ“tn]
    exact t_sub âŸ¨le_rfl, t_mono n.le_succâŸ©
  Â· rw [closure_le_eq continuous_id' continuous_const] at h; exact âŸ¨h.1.1, h.2âŸ©
  Â· apply q.continuousOn_invFun.comp ((Continuous.prodMk_left _).comp Î³.2).continuousOn
    simp_rw [not_le, q.target_eq]; intro s h
    exact âŸ¨t_sub âŸ¨closure_lt_subset_le continuous_const continuous_subtype_val h.2, h.1.2âŸ©, âŸ¨âŸ©âŸ©
  Â· rw [Function.comp_apply]; split_ifs with h
    exacts [eqOn âŸ¨hs.1, hâŸ©, q.proj_symm_apply' (t_sub âŸ¨le_of_not_ge h, hs.2âŸ©)]
  Â· dsimp only; rwa [if_pos (t_0 â–¸ t_mono n.zero_le)]

/-- The lift of a path to a covering space given a lift of the left endpoint. -/
noncomputable def liftPath : C(I, E) := (cov.exists_path_lifts Î³ e Î³_0).choose

lemma liftPath_lifts : p âˆ˜ cov.liftPath Î³ e Î³_0 = Î³ := (cov.exists_path_lifts Î³ e Î³_0).choose_spec.1
lemma liftPath_zero : cov.liftPath Î³ e Î³_0 0 = e := (cov.exists_path_lifts Î³ e Î³_0).choose_spec.2

variable {Î³ e}
lemma eq_liftPath_iff {Î“ : I â†’ E} : Î“ = cov.liftPath Î³ e Î³_0 â†” Continuous Î“ âˆ§ p âˆ˜ Î“ = Î³ âˆ§ Î“ 0 = e :=
  have lifts := cov.liftPath_lifts Î³ e Î³_0
  have zero := cov.liftPath_zero Î³ e Î³_0
  âŸ¨(Â· â–¸ âŸ¨(cov.liftPath Î³ e Î³_0).2, lifts, zeroâŸ©), fun âŸ¨Î“_cont, Î“_lifts, Î“_0âŸ© â†¦ cov.eq_of_comp_eq
    Î“_cont (cov.liftPath Î³ e Î³_0).continuous (Î“_lifts â–¸ lifts.symm) 0 (Î“_0 â–¸ zero.symm)âŸ©

/-- Unique characterization of the lifted path. -/
lemma eq_liftPath_iff' {Î“ : C(I, E)} : Î“ = cov.liftPath Î³ e Î³_0 â†” p âˆ˜ Î“ = Î³ âˆ§ Î“ 0 = e := by
  simp_rw [â† DFunLike.coe_fn_eq, eq_liftPath_iff, and_iff_right (ContinuousMap.continuous _)]

omit Î³_0
lemma liftPath_const {x : X} (hpe : x = p e) : cov.liftPath (.const I x) e hpe = .const I e :=
  .symm <| (cov.eq_liftPath_iff' _).mpr âŸ¨funext fun _ â†¦ hpe.symm, rflâŸ©

lemma liftPath_trans {x y z : X} {e : E} (hpe : x = p e) (Î³ : Path x y) (Î³' : Path y z) :
    letI Î“ := cov.liftPath Î³ e (Î³.source.trans hpe)
    cov.liftPath (Î³.trans Î³') e (by simpa) = (âŸ¨Î“, liftPath_zero .., rflâŸ© : Path e (Î“ 1)).trans
      âŸ¨cov.liftPath Î³' (Î“ 1) (by simpa using (congr_fun (cov.liftPath_lifts Î³ ..) 1).symm),
        liftPath_zero .., rflâŸ© := by
  refine .symm <| (cov.eq_liftPath_iff' _).mpr âŸ¨funext fun _ â†¦ ?_, by simpâŸ©
  simp only [ContinuousMap.coe_coe, Function.comp_apply, Path.trans_apply]; split_ifs
  Â· exact congr_fun (cov.liftPath_lifts Î³ e (Î³.source.trans hpe)) _
  Â· refine congr_fun (cov.liftPath_lifts Î³' _ ?_) _
    simpa using (congr_fun (cov.liftPath_lifts Î³ ..) 1).symm

end path_lifting

section homotopy_lifting
variable (H : C(I Ã— A, X)) (f : C(A, E)) (H_0 : âˆ€ a, H (0, a) = p (f a))

/-- The existence of `liftHomotopy` satisfying `liftHomotopy_lifts` and `liftHomotopy_zero` is
  the homotopy lifting property for covering maps.
  In other words, a covering map is a Hurewicz fibration.
  Proposition 1.30 of [hatcher02]. -/
@[simps] noncomputable def liftHomotopy : C(I Ã— A, E) where
  toFun ta := cov.liftPath (H.comp <| (ContinuousMap.id I).prodMk <| .const I ta.2)
    (f ta.2) (H_0 ta.2) ta.1
  continuous_toFun := cov.isLocalHomeomorph.continuous_lift cov.isSeparatedMap H
    (by ext âŸ¨t, aâŸ©; exact congr_fun (cov.liftPath_lifts ..) t)
    (by convert f.continuous with a; exact cov.liftPath_zero ..)
    fun a â†¦ by dsimp only; exact (cov.liftPath (Î³_0 := by simp [*])).2

lemma liftHomotopy_lifts : p âˆ˜ cov.liftHomotopy H f H_0 = H :=
  funext fun âŸ¨t, _âŸ© â†¦ congr_fun (cov.liftPath_lifts ..) t

lemma liftHomotopy_zero (a : A) : cov.liftHomotopy H f H_0 (0, a) = f a := cov.liftPath_zero ..

variable {H f}
lemma eq_liftHomotopy_iff (H' : I Ã— A â†’ E) : H' = cov.liftHomotopy H f H_0 â†”
    (âˆ€ a, Continuous (H' âŸ¨Â·, aâŸ©)) âˆ§ p âˆ˜ H' = H âˆ§ âˆ€ a, H' (0, a) = f a := by
  refine âŸ¨?_, fun âŸ¨H'_cont, H'_lifts, H'_0âŸ© â†¦ funext fun âŸ¨t, aâŸ© â†¦ ?_âŸ©
  Â· rintro rfl; refine âŸ¨fun a â†¦ ?_, cov.liftHomotopy_lifts H f H_0, cov.liftHomotopy_zero H f H_0âŸ©
    simp_rw [liftHomotopy_apply]; exact (cov.liftPath _ _ <| H_0 a).2
  Â· apply congr_fun ((cov.eq_liftPath_iff _).mpr âŸ¨H'_cont a, _, H'_0 aâŸ©) t
    ext âŸ¨t, aâŸ©; exact congr_fun H'_lifts _

lemma eq_liftHomotopy_iff' (H' : C(I Ã— A, E)) :
    H' = cov.liftHomotopy H f H_0 â†” p âˆ˜ H' = H âˆ§ âˆ€ a, H' (0, a) = f a := by
  simp_rw [â† DFunLike.coe_fn_eq, eq_liftHomotopy_iff]
  exact and_iff_right fun a â†¦ H'.2.comp (.prodMk_left a)

variable {fâ‚€ fâ‚ : C(A, X)} {S : Set A} (F : fâ‚€.HomotopyRel fâ‚ S)

open ContinuousMap in
/-- The lift to a covering space of a homotopy between two continuous maps relative to a set
given compatible lifts of the continuous maps. -/
noncomputable def liftHomotopyRel [PreconnectedSpace A]
    {fâ‚€' fâ‚' : C(A, E)} (he : âˆƒ a âˆˆ S, fâ‚€' a = fâ‚' a)
    (hâ‚€ : p âˆ˜ fâ‚€' = fâ‚€) (hâ‚ : p âˆ˜ fâ‚' = fâ‚) : fâ‚€'.HomotopyRel fâ‚' S :=
  have F_0 : âˆ€ a, F (0, a) = p (fâ‚€' a) := fun a â†¦ (F.apply_zero a).trans (congr_fun hâ‚€ a).symm
  have rel : âˆ€ t, âˆ€ a âˆˆ S, cov.liftHomotopy F fâ‚€' F_0 (t, a) = fâ‚€' a := fun t a ha â†¦ by
    rw [liftHomotopy_apply, cov.const_of_comp (ContinuousMap.continuous _) _ t 0]
    Â· apply cov.liftPath_zero
    Â· intro t t'; simp_rw [â† p.comp_apply, cov.liftPath_lifts]
      exact (F.prop t a ha).trans (F.prop t' a ha).symm
  { toContinuousMap := cov.liftHomotopy F fâ‚€' F_0
    map_zero_left := cov.liftHomotopy_zero F fâ‚€' F_0
    map_one_left := by
      obtain âŸ¨a, ha, heâŸ© := he
      simp_rw [toFun_eq_coe, â† curry_apply]
      refine congr_fun (cov.eq_of_comp_eq
        (ContinuousMap.continuous _) fâ‚'.continuous ?_ a <| (rel 1 a ha).trans he)
      ext a; rw [hâ‚, Function.comp_apply, curry_apply]
      exact (congr_fun (cov.liftHomotopy_lifts F fâ‚€' _) (1, a)).trans (F.apply_one a)
    prop' := rel }

/-- Two continuous maps from a preconnected space to the total space of a covering map
  are homotopic relative to a set `S` if and only if their compositions with the covering map
  are homotopic relative to `S`, assuming that they agree at a point in `S`. -/
theorem homotopicRel_iff_comp [PreconnectedSpace A] {fâ‚€ fâ‚ : C(A, E)} {S : Set A}
    (he : âˆƒ a âˆˆ S, fâ‚€ a = fâ‚ a) : fâ‚€.HomotopicRel fâ‚ S â†”
      (ContinuousMap.comp âŸ¨p, cov.continuousâŸ© fâ‚€).HomotopicRel (.comp âŸ¨p, cov.continuousâŸ© fâ‚) S :=
  âŸ¨fun âŸ¨FâŸ© â†¦ âŸ¨F.compContinuousMap _âŸ©, fun âŸ¨FâŸ© â†¦ âŸ¨cov.liftHomotopyRel F he rfl rflâŸ©âŸ©

/-- Lifting two paths that are homotopic relative to {0,1}
  starting from the same point also ends up in the same point. -/
theorem liftPath_apply_one_eq_of_homotopicRel {Î³â‚€ Î³â‚ : C(I, X)}
    (h : Î³â‚€.HomotopicRel Î³â‚ {0,1}) (e : E) (hâ‚€ : Î³â‚€ 0 = p e) (hâ‚ : Î³â‚ 0 = p e) :
    cov.liftPath Î³â‚€ e hâ‚€ 1 = cov.liftPath Î³â‚ e hâ‚ 1 := by
  obtain âŸ¨HâŸ© := h
  have := cov.liftHomotopyRel (fâ‚€' := cov.liftPath Î³â‚€ e hâ‚€) (fâ‚' := cov.liftPath Î³â‚ e hâ‚) H
    âŸ¨0, .inl rfl, by simp_rw [liftPath_zero]âŸ© (liftPath_lifts ..) (liftPath_lifts ..)
  rw [â† this.eq_fst 0 (.inr rfl), â† this.eq_snd 0 (.inr rfl)]

/-- The monodromy of a covering map `p : E â†’ X`, which sends a lift of the starting point of a
  path in `X` to the endpoint of the lifted path in `E`. It only depends on the homotopy class
  of the path. -/
noncomputable def monodromy {x y : X} (Î³ : Path.Homotopic.Quotient x y) :
    p â»Â¹' {x} â†’ p â»Â¹' {y} :=
  fun e â†¦ Î³.lift (fun Î³ : Path x y â†¦ âŸ¨cov.liftPath Î³ e (Î³.source.trans e.2.symm) 1,
      (congr_fun (cov.liftPath_lifts ..) 1).trans Î³.targetâŸ©)
    fun _ _ h â†¦ Subtype.ext (cov.liftPath_apply_one_eq_of_homotopicRel h ..)

theorem monodromy_map {x y : E} (Î³ : Path.Homotopic.Quotient x y) :
    cov.monodromy (Î³.map âŸ¨p, cov.continuousâŸ©) âŸ¨x, rflâŸ© = âŸ¨y, rflâŸ© := Subtype.ext <| by
  obtain âŸ¨Î³âŸ© := Î³
  exact (DFunLike.congr_fun ((cov.eq_liftPath_iff' _).mpr âŸ¨rfl, Î³.sourceâŸ©).symm 1).trans Î³.target

theorem monodromy_refl {x : X} : cov.monodromy (.refl x) = id :=
  funext fun e â†¦ Subtype.ext <| DFunLike.congr_fun (cov.liftPath_const e.2.symm) 1

theorem monodromy_trans_apply {x y z : X}
    (Î³ : Path.Homotopic.Quotient x y) (Î³' : Path.Homotopic.Quotient y z) (e) :
    cov.monodromy (Î³.trans Î³') e = cov.monodromy Î³' (cov.monodromy Î³ e) := by
  obtain âŸ¨Î³âŸ© := Î³; obtain âŸ¨Î³'âŸ© := Î³'
  exact Subtype.ext ((DFunLike.congr_fun (cov.liftPath_trans e.2.symm ..) 1).trans (Path.target _))

open CategoryTheory

/-- Monodromy of a covering map as a functor. Definition 2.1 in
https://ncatlab.org/nlab/show/monodromy. -/
@[simps] noncomputable def monodromyFunctor : FundamentalGroupoid X â¥¤ Type _ where
  obj x := p â»Â¹' {x.as}
  map := cov.monodromy
  map_id _ := cov.monodromy_refl
  map_comp _ _ := funext (cov.monodromy_trans_apply _ _)

theorem monodromy_bijective {x y : X} (Î³ : Path.Homotopic.Quotient x y) :
    (cov.monodromy Î³).Bijective :=
  (isIso_iff_bijective _).mp (cov.monodromyFunctor.map_isIso _)

/-- A covering map induces an injection on all Hom-sets of the fundamental groupoid,
  in particular on the fundamental group. The first part of Proposition 1.31 of [hatcher02]. -/
lemma injective_path_homotopic_map (eâ‚€ eâ‚ : E) :
    Function.Injective fun Î³ : Path.Homotopic.Quotient eâ‚€ eâ‚ â†¦ Î³.map âŸ¨p, cov.continuousâŸ© := by
  refine Quotient.indâ‚‚ fun Î³â‚€ Î³â‚ â†¦ ?_
  dsimp only
  simp only [Path.Homotopic.Quotient.mk''_eq_mk]
  simp_rw [â† Path.Homotopic.Quotient.mk_map]
  iterate 2 rw [Path.Homotopic.Quotient.eq]
  exact (cov.homotopicRel_iff_comp âŸ¨0, .inl rfl, Î³â‚€.source.trans Î³â‚.source.symmâŸ©).mpr

@[deprecated (since := "2025-11-20")]
alias injective_path_homotopic_mapFn := injective_path_homotopic_map

/-- A continuous map `f` from a simply-connected, locally path-connected space `A` to another
  space `X` lifts uniquely through a covering map `p : E â†’ X`, after specifying any lift
  `eâ‚€ : E` of any point `aâ‚€ : A`. -/
theorem existsUnique_continuousMap_lifts [SimplyConnectedSpace A] [LocPathConnectedSpace A]
    (f : C(A, X)) (aâ‚€ : A) (eâ‚€ : E) (he : p eâ‚€ = f aâ‚€) :
    âˆƒ! F : C(A, E), F aâ‚€ = eâ‚€ âˆ§ p âˆ˜ F = f := by
  refine cov.isLocalHomeomorph.existsUnique_continuousMap_lifts f aâ‚€ eâ‚€ he (fun Î³ Î³_0 â†¦ ?_)
    fun Î³ Î³' Î“ Î“' Î³_0 Î³'_0 Î“_0 Î“'_0 Î“_lifts Î“'_lifts Î³Î³'1 â†¦ ?_
  Â· simpa [and_comm] using cov.exists_path_lifts (f.comp Î³) eâ‚€ (by simp [Î³_0, he])
  let pÎ³ : Path aâ‚€ (Î³ 1) := âŸ¨Î³, Î³_0, rflâŸ©
  let pÎ³' : Path aâ‚€ (Î³ 1) := âŸ¨Î³', Î³'_0, Î³Î³'1.symmâŸ©
  convert cov.liftPath_apply_one_eq_of_homotopicRel (ContinuousMap.HomotopicRel.comp_continuousMap
    (SimplyConnectedSpace.paths_homotopic pÎ³ pÎ³') f) eâ‚€ (by simp [he]) (by simp [he]) <;>
    rw [eq_liftPath_iff']
  exacts [âŸ¨Î“_lifts, Î“_0âŸ©, âŸ¨Î“'_lifts, Î“'_0âŸ©]

open FundamentalGroup Path.Homotopic.Quotient in
/-- A continuous map `f` from a path connected, locally path-connected space `A` to another
  space `X` lifts uniquely through a covering map `p : E â†’ X` (such that `f aâ‚€` is lifted to `eâ‚€`)
  if `fâ Ï€â‚(A, aâ‚€) âŠ† pâ Ï€â‚(E, eâ‚€)`. Proposition 1.33 of [hatcher02], known as
  the lifting criterion. -/
theorem existsUnique_continuousMap_lifts_of_range_le
    [PathConnectedSpace A] [LocPathConnectedSpace A]
    {f : C(A, X)} {aâ‚€ : A} {eâ‚€ : E} (he : p eâ‚€ = f aâ‚€)
    (le : (map f aâ‚€).range â‰¤ (mapOfEq âŸ¨p, cov.continuousâŸ© he).range) :
    âˆƒ! F : C(A, E), F aâ‚€ = eâ‚€ âˆ§ p âˆ˜ F = f := by
  refine cov.isLocalHomeomorph.existsUnique_continuousMap_lifts f aâ‚€ eâ‚€ he (fun Î³ Î³_0 â†¦ ?_)
    fun Î³ Î³' Î“ Î“' Î³_0 Î³'_0 Î“_0 Î“'_0 Î“_lifts Î“'_lifts Î³Î³'1 â†¦ ?_
  Â· simpa [and_comm] using cov.exists_path_lifts (f.comp Î³) eâ‚€ (by simp [Î³_0, he])
  rw [(cov.eq_liftPath_iff' <| by simp [Î³_0, he]).mpr âŸ¨Î“_lifts, Î“_0âŸ©,
    (cov.eq_liftPath_iff' <| by simp [Î³'_0, he]).mpr âŸ¨Î“'_lifts, Î“'_0âŸ©]
  let pÎ³ : Path aâ‚€ (Î³ 1) := âŸ¨Î³, Î³_0, rflâŸ©
  let pÎ³' : Path aâ‚€ (Î³ 1) := âŸ¨Î³', Î³'_0, Î³Î³'1.symmâŸ©
  change (cov.monodromy (.mk <| pÎ³.map f.continuous) âŸ¨eâ‚€, heâŸ©).1 =
    (cov.monodromy (.mk <| pÎ³'.map f.continuous) âŸ¨eâ‚€, heâŸ©).1
  rw [â† Subtype.ext_iff]
  apply (cov.monodromy_bijective <| .mk (pÎ³'.map f.continuous).symm).1
  simp_rw [â† monodromy_trans_apply, â† mk_trans]
  conv_rhs => rw [â† eq.2 âŸ¨.reflTransSymm _âŸ©, mk_refl, monodromy_refl]
  rw [Path.map_symm, â† Path.map_trans]
  set pÎ³Î³' : Path aâ‚€ aâ‚€ := pÎ³.trans pÎ³'.symm
  have âŸ¨pÎ“Î“', eqâŸ© := le âŸ¨fromPath (.mk pÎ³Î³'), rflâŸ©
  obtain âŸ¨pÎ“Î“', rflâŸ© := mk_surjective pÎ“Î“'
  rw [mapOfEq_apply, map_apply, â† mk_map] at eq
  exact eq â–¸ Subtype.ext (congr_arg (Â·.1) (cov.monodromy_map (.mk pÎ“Î“')))

end homotopy_lifting

end IsCoveringMap
