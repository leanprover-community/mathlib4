/-
Copyright (c) 2021 Shing Tak Lam. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Shing Tak Lam
-/
import Mathlib.Topology.Homotopy.Basic

#align_import topology.homotopy.equiv from "leanprover-community/mathlib"@"3d7987cda72abc473c7cdbbb075170e9ac620042"

/-!

# Homotopy equivalences between topological spaces

In this file, we define homotopy equivalences between topological spaces `X` and `Y` as a pair of
functions `f : C(X, Y)` and `g : C(Y, X)` such that `f.comp g` and `g.comp f` are both homotopic
to `ContinuousMap.id`.

## Main definitions

- `ContinuousMap.HomotopyEquiv` is the type of homotopy equivalences between topological spaces.

## Notation

We introduce the notation `X ‚âÉ‚Çï Y` for `ContinuousMap.HomotopyEquiv X Y` in the `ContinuousMap`
locale.

-/

universe u v w x

variable {X : Type u} {Y : Type v} {Z : Type w} {Z' : Type x}

variable [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z] [TopologicalSpace Z']

namespace ContinuousMap

/-- A homotopy equivalence between topological spaces `X` and `Y` are a pair of functions
`toFun : C(X, Y)` and `invFun : C(Y, X)` such that `toFun.comp invFun` and `invFun.comp toFun`
are both homotopic to corresponding identity maps.
-/
@[ext]
structure HomotopyEquiv (X : Type u) (Y : Type v) [TopologicalSpace X] [TopologicalSpace Y] where
  toFun : C(X, Y)
  invFun : C(Y, X)
  left_inv : (invFun.comp toFun).Homotopic (ContinuousMap.id X)
  right_inv : (toFun.comp invFun).Homotopic (ContinuousMap.id Y)
#align continuous_map.homotopy_equiv ContinuousMap.HomotopyEquiv

-- mathport name: continuous_map.homotopy_equiv
scoped infixl:25 " ‚âÉ‚Çï " => ContinuousMap.HomotopyEquiv

namespace HomotopyEquiv

/-- Coercion of a `HomotopyEquiv` to function. While the Lean 4 way is to unfold coercions, this
auxiliary definition will make porting of Lean 3 code easier.

Porting note: TODO: drop this definition. -/
@[coe] def toFun' (e : X ‚âÉ‚Çï Y) : X ‚Üí Y := e.toFun

instance : CoeFun (X ‚âÉ‚Çï Y) fun _ => X ‚Üí Y := ‚ü®toFun'‚ü©

@[simp]
theorem toFun_eq_coe (h : HomotopyEquiv X Y) : (h.toFun : X ‚Üí Y) = h :=
  rfl
#align continuous_map.homotopy_equiv.to_fun_eq_coe ContinuousMap.HomotopyEquiv.toFun_eq_coe

@[continuity]
theorem continuous (h : HomotopyEquiv X Y) : Continuous h :=
  h.toFun.continuous
#align continuous_map.homotopy_equiv.continuous ContinuousMap.HomotopyEquiv.continuous

end HomotopyEquiv

end ContinuousMap

open ContinuousMap

namespace Homeomorph

/-- Any homeomorphism is a homotopy equivalence.
-/
def toHomotopyEquiv (h : X ‚âÉ‚Çú Y) : X ‚âÉ‚Çï Y where
  toFun := h
  invFun := h.symm
  left_inv := by rw [symm_comp_toContinuousMap]
                 -- üéâ no goals
  right_inv := by rw [toContinuousMap_comp_symm]
                  -- üéâ no goals
#align homeomorph.to_homotopy_equiv Homeomorph.toHomotopyEquiv

@[simp]
theorem coe_toHomotopyEquiv (h : X ‚âÉ‚Çú Y) : (h.toHomotopyEquiv : X ‚Üí Y) = h :=
  rfl
#align homeomorph.coe_to_homotopy_equiv Homeomorph.coe_toHomotopyEquiv

end Homeomorph

namespace ContinuousMap

namespace HomotopyEquiv

/-- If `X` is homotopy equivalent to `Y`, then `Y` is homotopy equivalent to `X`.
-/
def symm (h : X ‚âÉ‚Çï Y) : Y ‚âÉ‚Çï X where
  toFun := h.invFun
  invFun := h.toFun
  left_inv := h.right_inv
  right_inv := h.left_inv
#align continuous_map.homotopy_equiv.symm ContinuousMap.HomotopyEquiv.symm

@[simp]
theorem coe_invFun (h : HomotopyEquiv X Y) : (‚áëh.invFun : Y ‚Üí X) = ‚áëh.symm :=
  rfl
#align continuous_map.homotopy_equiv.coe_inv_fun ContinuousMap.HomotopyEquiv.coe_invFun

/-- See Note [custom simps projection]. We need to specify this projection explicitly in this case,
because it is a composition of multiple projections. -/
def Simps.apply (h : X ‚âÉ‚Çï Y) : X ‚Üí Y :=
  h
#align continuous_map.homotopy_equiv.simps.apply ContinuousMap.HomotopyEquiv.Simps.apply

/-- See Note [custom simps projection]. We need to specify this projection explicitly in this case,
because it is a composition of multiple projections. -/
def Simps.symm_apply (h : X ‚âÉ‚Çï Y) : Y ‚Üí X :=
  h.symm
#align continuous_map.homotopy_equiv.simps.symm_apply ContinuousMap.HomotopyEquiv.Simps.symm_apply

initialize_simps_projections HomotopyEquiv (toFun_toFun ‚Üí apply, invFun_toFun ‚Üí symm_apply,
  -toFun, -invFun)

/-- Any topological space is homotopy equivalent to itself.
-/
@[simps!]
def refl (X : Type u) [TopologicalSpace X] : X ‚âÉ‚Çï X :=
  (Homeomorph.refl X).toHomotopyEquiv
#align continuous_map.homotopy_equiv.refl ContinuousMap.HomotopyEquiv.refl
#align continuous_map.homotopy_equiv.refl_apply ContinuousMap.HomotopyEquiv.refl_apply
#align continuous_map.homotopy_equiv.refl_symm_apply ContinuousMap.HomotopyEquiv.refl_symm_apply

instance : Inhabited (HomotopyEquiv Unit Unit) :=
  ‚ü®refl Unit‚ü©

/--
If `X` is homotopy equivalent to `Y`, and `Y` is homotopy equivalent to `Z`, then `X` is homotopy
equivalent to `Z`.
-/
@[simps!]
def trans (h‚ÇÅ : X ‚âÉ‚Çï Y) (h‚ÇÇ : Y ‚âÉ‚Çï Z) : X ‚âÉ‚Çï Z where
  toFun := h‚ÇÇ.toFun.comp h‚ÇÅ.toFun
  invFun := h‚ÇÅ.invFun.comp h‚ÇÇ.invFun
  left_inv := by
    refine Homotopic.trans ?_ h‚ÇÅ.left_inv
    -- ‚ä¢ Homotopic (comp (comp h‚ÇÅ.invFun h‚ÇÇ.invFun) (comp h‚ÇÇ.toFun h‚ÇÅ.toFun)) (comp h ‚Ä¶
    exact ((Homotopic.refl _).hcomp h‚ÇÇ.left_inv).hcomp (Homotopic.refl _)
    -- üéâ no goals
  right_inv := by
    refine Homotopic.trans ?_ h‚ÇÇ.right_inv
    -- ‚ä¢ Homotopic (comp (comp h‚ÇÇ.toFun h‚ÇÅ.toFun) (comp h‚ÇÅ.invFun h‚ÇÇ.invFun)) (comp h ‚Ä¶
    exact ((Homotopic.refl _).hcomp h‚ÇÅ.right_inv).hcomp (Homotopic.refl _)
    -- üéâ no goals
#align continuous_map.homotopy_equiv.trans ContinuousMap.HomotopyEquiv.trans
#align continuous_map.homotopy_equiv.trans_apply ContinuousMap.HomotopyEquiv.trans_apply
#align continuous_map.homotopy_equiv.trans_symm_apply ContinuousMap.HomotopyEquiv.trans_symm_apply

theorem symm_trans (h‚ÇÅ : X ‚âÉ‚Çï Y) (h‚ÇÇ : Y ‚âÉ‚Çï Z) : (h‚ÇÅ.trans h‚ÇÇ).symm = h‚ÇÇ.symm.trans h‚ÇÅ.symm := rfl
#align continuous_map.homotopy_equiv.symm_trans ContinuousMap.HomotopyEquiv.symm_trans

/-- If `X` is homotopy equivalent to `Y` and `Z` is homotopy equivalent to `Z'`, then `X √ó Z` is
homotopy equivalent to `Z √ó Z'`. -/
def prodCongr (h‚ÇÅ : X ‚âÉ‚Çï Y) (h‚ÇÇ : Z ‚âÉ‚Çï Z') : (X √ó Z) ‚âÉ‚Çï (Y √ó Z') where
  toFun := h‚ÇÅ.toFun.prodMap h‚ÇÇ.toFun
  invFun := h‚ÇÅ.invFun.prodMap h‚ÇÇ.invFun
  left_inv := h‚ÇÅ.left_inv.prodMap h‚ÇÇ.left_inv
  right_inv := h‚ÇÅ.right_inv.prodMap h‚ÇÇ.right_inv

/-- If `X i` is homotopy equivalent to `Y i` for each `i`, then the space of functions (a.k.a. the
indexed product) `‚àÄ i, X i` is homotopy equivalent to `‚àÄ i, Y i`. -/
def piCongrRight {Œπ : Type*} {X Y : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (X i)]
    [‚àÄ i, TopologicalSpace (Y i)] (h : ‚àÄ i, X i ‚âÉ‚Çï Y i) :
    (‚àÄ i, X i) ‚âÉ‚Çï (‚àÄ i, Y i) where
  toFun := .piMap fun i ‚Ü¶ (h i).toFun
  invFun := .piMap fun i ‚Ü¶ (h i).invFun
  left_inv := .piMap fun i ‚Ü¶ (h i).left_inv
  right_inv := .piMap fun i ‚Ü¶ (h i).right_inv

end HomotopyEquiv

end ContinuousMap

open ContinuousMap

namespace Homeomorph

@[simp]
theorem refl_toHomotopyEquiv (X : Type u) [TopologicalSpace X] :
    (Homeomorph.refl X).toHomotopyEquiv = HomotopyEquiv.refl X :=
  rfl
#align homeomorph.refl_to_homotopy_equiv Homeomorph.refl_toHomotopyEquiv

@[simp]
theorem symm_toHomotopyEquiv (h : X ‚âÉ‚Çú Y) : h.symm.toHomotopyEquiv = h.toHomotopyEquiv.symm :=
  rfl
#align homeomorph.symm_to_homotopy_equiv Homeomorph.symm_toHomotopyEquiv

@[simp]
theorem trans_toHomotopyEquiv (h‚ÇÄ : X ‚âÉ‚Çú Y) (h‚ÇÅ : Y ‚âÉ‚Çú Z) :
    (h‚ÇÄ.trans h‚ÇÅ).toHomotopyEquiv = h‚ÇÄ.toHomotopyEquiv.trans h‚ÇÅ.toHomotopyEquiv :=
  rfl
#align homeomorph.trans_to_homotopy_equiv Homeomorph.trans_toHomotopyEquiv

end Homeomorph
