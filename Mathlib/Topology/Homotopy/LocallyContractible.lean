/-
Copyright (c) 2025 Lean FRO, LLC. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
module

public import Mathlib.Topology.Homotopy.Contractible
public import Mathlib.Topology.Homotopy.Basic
public import Mathlib.Topology.Connected.LocPathConnected
public import Mathlib.Topology.Homeomorph.Lemmas

/-!
# Strongly locally contractible spaces

This file defines `LocallyContractibleSpace` and `StronglyLocallyContractibleSpace`.

## Main definitions

* `LocallyContractibleSpace X`: classical local contractibility (null-homotopic inclusions).
* `StronglyLocallyContractibleSpace X`: a space where each point has a neighborhood basis
  consisting of contractible sets (not necessarily open).

## Main results

* `StronglyLocallyContractibleSpace.locallyContractible`: SLC implies classical LC
* `instLocPathConnectedSpace`: strongly locally contractible spaces are locally path-connected
* `StronglyLocallyContractibleSpace.of_bases`: a helper to construct strongly locally contractible
  spaces from a neighborhood basis
* `contractible_subset_basis`: basis of contractible neighborhoods contained in an open set
* `IsOpenEmbedding.stronglyLocallyContractibleSpace`: open embeddings preserve strong local
  contractibility
* `IsOpen.stronglyLocallyContractibleSpace`: open subsets of strongly locally contractible spaces
  are strongly locally contractible
* Products of strongly locally contractible spaces are strongly locally contractible

## TODO

* Define contractible components and prove they are open in strongly locally contractible spaces
* Add examples: convex sets, real vector spaces, star-shaped sets

## Notes

**Terminology:** The classical definition of *locally contractible* (LC) requires that for every
point `x` and neighborhood `U ‚àã x`, there exists a neighborhood `V ‚àã x` with `V ‚äÜ U` such that the
inclusion `V ‚Ü™ U` is null-homotopic. The definition here is **strictly stronger**: we require
contractible neighborhoods to form a neighborhood basis. This is often called **strongly locally
contractible** (SLC).

**Hierarchy of notions:**
* "Basis of open contractible neighborhoods" (strongest)
* "Basis of contractible neighborhoods" (this file, SLC)
* "Null-homotopic inclusions" (classical LC, weakest)

This naming is not used uniformly: according to
https://ncatlab.org/nlab/show/locally+contractible+space
the second and third notion here could also be called
"locally contractible" and "semilocally contractible" respectively.
We've enquired at
https://math.stackexchange.com/questions/5109428/terminology-for-local-contractibility-locally-contractible-vs-strongly-local
in the hope of getting definitive naming advice.

The Borsuk-Mazurkiewicz counterexample [borsuk_mazurkiewicz1934] shows that classical LC does not
imply SLC. Moreover, from a contractible neighborhood `S` one generally cannot shrink to an open
`V ‚äÜ S` that remains contractible, so requiring neighborhoods to be open is potentially strictly
stronger than SLC.
-/

@[expose] public section

noncomputable section

open Topology Filter Set Function ContinuousMap

variable {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] {x y : X} {Œπ : Type*}

section LocallyContractible

/-- Classical **local contractibility**: for every point and every neighborhood U,
there exists a neighborhood V ‚äÜ U such that the inclusion V ‚Ü™ U is null-homotopic.

This is weaker than `StronglyLocallyContractibleSpace`. -/
def LocallyContractibleSpace (X : Type*) [TopologicalSpace X] : Prop :=
  ‚àÄ (x : X) (U : Set X), U ‚àà ùìù x ‚Üí
    ‚àÉ (V : Set X) (hVU : V ‚äÜ U), V ‚àà ùìù x ‚àß Nullhomotopic (inclusion hVU)

end LocallyContractible

section StronglyLocallyContractibleSpace

/-- A topological space is **strongly locally contractible** if, at every point, contractible
neighborhoods form a neighborhood basis. Here "contractible" means contractible as a subspace.

This is strictly stronger than the classical notion of locally contractible, which only requires
null-homotopic inclusions.
This distinction is witnessed by an example from Borsuk-Mazurkiewicz [borsuk_mazurkiewicz1934];
see also [MO88628] for discussion and the Whitehead manifold example. -/
class StronglyLocallyContractibleSpace (X : Type*) [TopologicalSpace X] : Prop where
  /-- Each neighborhood filter has a basis of contractible subspace neighborhoods. -/
  contractible_basis : ‚àÄ x : X,
    (ùìù x).HasBasis (fun s : Set X => s ‚àà ùìù x ‚àß ContractibleSpace s) id

export StronglyLocallyContractibleSpace (contractible_basis)

/-- A helper to construct a strongly locally contractible space from a neighborhood basis where
each basis element is contractible as a subspace. -/
theorem StronglyLocallyContractibleSpace.of_bases {p : X ‚Üí Œπ ‚Üí Prop} {s : X ‚Üí Œπ ‚Üí Set X}
    (h : ‚àÄ x, (ùìù x).HasBasis (p x) (s x)) (h' : ‚àÄ x i, p x i ‚Üí ContractibleSpace (s x i)) :
    StronglyLocallyContractibleSpace X where
  contractible_basis x := by
    rw [hasBasis_self]
    intro t ht
    obtain ‚ü®i, hpi, hi‚ü© := (h x).mem_iff.mp ht
    exact ‚ü®s x i, (h x).mem_of_mem hpi, h' x i hpi, hi‚ü©

variable [StronglyLocallyContractibleSpace X]

/-- In a strongly locally contractible space, for any open set `U` containing `x`, there is a basis
of contractible neighborhoods of `x` contained in `U`. -/
theorem contractible_subset_basis {U : Set X} (h : IsOpen U) (hx : x ‚àà U) :
    (ùìù x).HasBasis (fun s : Set X => s ‚àà ùìù x ‚àß ContractibleSpace s ‚àß s ‚äÜ U) id :=
  (contractible_basis x).hasBasis_self_subset (IsOpen.mem_nhds h hx)

/-- Strongly locally contractible spaces are locally path-connected. -/
instance (priority := 100) instLocPathConnectedSpace : LocPathConnectedSpace X where
  path_connected_basis x := by
    refine contractible_basis x |>.to_hasBasis'
      (fun s ‚ü®hs, hs'‚ü© ‚Ü¶ ‚ü®s, ‚ü®hs, ?_‚ü©, le_rfl‚ü©) (fun s hs ‚Ü¶ hs.1)
    rw [isPathConnected_iff_pathConnectedSpace]
    infer_instance

/-- Open embeddings preserve strong local contractibility: if `X` is strongly locally contractible
and `e : Y ‚Üí X` is an open embedding, then `Y` is strongly locally contractible. -/
theorem Topology.IsOpenEmbedding.stronglyLocallyContractibleSpace {e : Y ‚Üí X}
    (he : IsOpenEmbedding e) : StronglyLocallyContractibleSpace Y :=
  .of_bases
    (fun _ ‚Ü¶ he.basis_nhds <| contractible_subset_basis he.isOpen_range (mem_range_self _))
    fun _ _ ‚ü®_, hs, hse‚ü© ‚Ü¶
      (he.toIsEmbedding.homeomorphOfSubsetRange hse).contractibleSpace_iff.mpr hs

/-- Open subsets of strongly locally contractible spaces are strongly locally contractible. -/
theorem IsOpen.stronglyLocallyContractibleSpace {U : Set X} (h : IsOpen U) :
    StronglyLocallyContractibleSpace U :=
  h.isOpenEmbedding_subtypeVal.stronglyLocallyContractibleSpace

end StronglyLocallyContractibleSpace

section Products

/-- The product of two strongly locally contractible spaces is strongly locally contractible. -/
instance [StronglyLocallyContractibleSpace X] [StronglyLocallyContractibleSpace Y] :
    StronglyLocallyContractibleSpace (X √ó Y) := by
  refine .of_bases (Œπ := Set X √ó Set Y)
    (p := fun (x, y) (Ux, Uy) =>
      (Ux ‚àà ùìù x ‚àß ContractibleSpace Ux) ‚àß (Uy ‚àà ùìù y ‚àß ContractibleSpace Uy))
    (s := fun _ (Ux, Uy) => Ux √óÀ¢ Uy) ?_ ?_
  ¬∑ intro (x, y)
    rw [nhds_prod_eq]
    exact (contractible_basis x).prod (contractible_basis y)
  ¬∑ intro (x, y) (Ux, Uy) ‚ü®hUx, hUy‚ü©
    haveI : ContractibleSpace Ux := hUx.2
    haveI : ContractibleSpace Uy := hUy.2
    exact (Homeomorph.Set.prod Ux Uy).contractibleSpace

end Products

section Implications

/-- The strong notion (contractible neighborhood basis)
implies the classical notion (null-homotopic inclusions).
The converse is false by the Borsuk-Mazurkiewicz counterexample [borsuk_mazurkiewicz1934];
see also [MO88628] for discussion and the Whitehead manifold example. -/
theorem StronglyLocallyContractibleSpace.locallyContractible [StronglyLocallyContractibleSpace X] :
    LocallyContractibleSpace X := by
  intro x U hU
  obtain ‚ü®V, ‚ü®hVmem, hVcontractible‚ü©, hVU‚ü© := (contractible_basis x).mem_iff.mp hU
  refine ‚ü®V, hVU, hVmem, ?_‚ü©
  -- V is contractible, so the identity on V is nullhomotopic to a constant map
  obtain ‚ü®v‚ÇÄ, hid‚ü© := id_nullhomotopic V
  -- The inclusion V ‚Ü™ U is homotopic to the constant map at (inclusion v‚ÇÄ)
  refine ‚ü®ContinuousMap.inclusion hVU v‚ÇÄ, ?_‚ü©
  convert Homotopic.comp (.refl _) hid
  ext
  simp

end Implications
