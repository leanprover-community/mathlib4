/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro
-/
import Mathlib.Topology.Continuous
import Mathlib.Topology.Defs.Induced

/-!
# Ordering on topologies and (co)induced topologies

Topologies on a fixed type `Œ±` are ordered, by reverse inclusion.  That is, for topologies `t‚ÇÅ` and
`t‚ÇÇ` on `Œ±`, we write `t‚ÇÅ ‚â§ t‚ÇÇ` if every set open in `t‚ÇÇ` is also open in `t‚ÇÅ`.  (One also calls
`t‚ÇÅ` *finer* than `t‚ÇÇ`, and `t‚ÇÇ` *coarser* than `t‚ÇÅ`.)

Any function `f : Œ± ‚Üí Œ≤` induces

* `TopologicalSpace.induced f : TopologicalSpace Œ≤ ‚Üí TopologicalSpace Œ±`;
* `TopologicalSpace.coinduced f : TopologicalSpace Œ± ‚Üí TopologicalSpace Œ≤`.

Continuity, the ordering on topologies and (co)induced topologies are related as follows:

* The identity map `(Œ±, t‚ÇÅ) ‚Üí (Œ±, t‚ÇÇ)` is continuous iff `t‚ÇÅ ‚â§ t‚ÇÇ`.
* A map `f : (Œ±, t) ‚Üí (Œ≤, u)` is continuous
  * iff `t ‚â§ TopologicalSpace.induced f u` (`continuous_iff_le_induced`)
  * iff `TopologicalSpace.coinduced f t ‚â§ u` (`continuous_iff_coinduced_le`).

Topologies on `Œ±` form a complete lattice, with `‚ä•` the discrete topology and `‚ä§` the indiscrete
topology.

For a function `f : Œ± ‚Üí Œ≤`, `(TopologicalSpace.coinduced f, TopologicalSpace.induced f)` is a Galois
connection between topologies on `Œ±` and topologies on `Œ≤`.

## Implementation notes

There is a Galois insertion between topologies on `Œ±` (with the inclusion ordering) and all
collections of sets in `Œ±`. The complete lattice structure on topologies on `Œ±` is defined as the
reverse of the one obtained via this Galois insertion. More precisely, we use the corresponding
Galois coinsertion between topologies on `Œ±` (with the reversed inclusion ordering) and collections
of sets in `Œ±` (with the reversed inclusion ordering).

## Tags

finer, coarser, induced topology, coinduced topology
-/

open Function Set Filter Topology

universe u v w

namespace TopologicalSpace

variable {Œ± : Type u}

/-- The open sets of the least topology containing a collection of basic sets. -/
inductive GenerateOpen (g : Set (Set Œ±)) : Set Œ± ‚Üí Prop
  | basic : ‚àÄ s ‚àà g, GenerateOpen g s
  | univ : GenerateOpen g univ
  | inter : ‚àÄ s t, GenerateOpen g s ‚Üí GenerateOpen g t ‚Üí GenerateOpen g (s ‚à© t)
  | sUnion : ‚àÄ S : Set (Set Œ±), (‚àÄ s ‚àà S, GenerateOpen g s) ‚Üí GenerateOpen g (‚ãÉ‚ÇÄ S)

/-- The smallest topological space containing the collection `g` of basic sets -/
def generateFrom (g : Set (Set Œ±)) : TopologicalSpace Œ± where
  IsOpen := GenerateOpen g
  isOpen_univ := GenerateOpen.univ
  isOpen_inter := GenerateOpen.inter
  isOpen_sUnion := GenerateOpen.sUnion

theorem isOpen_generateFrom_of_mem {g : Set (Set Œ±)} {s : Set Œ±} (hs : s ‚àà g) :
    IsOpen[generateFrom g] s :=
  GenerateOpen.basic s hs

theorem nhds_generateFrom {g : Set (Set Œ±)} {a : Œ±} :
    @nhds Œ± (generateFrom g) a = ‚®Ö s ‚àà { s | a ‚àà s ‚àß s ‚àà g }, ùìü s := by
  letI := generateFrom g
  rw [nhds_def]
  refine le_antisymm (biInf_mono fun s ‚ü®as, sg‚ü© => ‚ü®as, .basic _ sg‚ü©) <| le_iInf‚ÇÇ ?_
  rintro s ‚ü®ha, hs‚ü©
  induction hs with
  | basic _ hs => exact iInf‚ÇÇ_le _ ‚ü®ha, hs‚ü©
  | univ => exact le_top.trans_eq principal_univ.symm
  | inter _ _ _ _ hs ht => exact (le_inf (hs ha.1) (ht ha.2)).trans_eq inf_principal
  | sUnion _ _ hS =>
    let ‚ü®t, htS, hat‚ü© := ha
    exact (hS t htS hat).trans (principal_mono.2 <| subset_sUnion_of_mem htS)

lemma tendsto_nhds_generateFrom_iff {Œ≤ : Type*} {m : Œ± ‚Üí Œ≤} {f : Filter Œ±} {g : Set (Set Œ≤)}
    {b : Œ≤} : Tendsto m f (@nhds Œ≤ (generateFrom g) b) ‚Üî ‚àÄ s ‚àà g, b ‚àà s ‚Üí m ‚Åª¬π' s ‚àà f := by
  simp only [nhds_generateFrom, @forall_swap (b ‚àà _), tendsto_iInf, mem_setOf_eq, and_imp,
    tendsto_principal]; rfl

/-- Construct a topology on Œ± given the filter of neighborhoods of each point of Œ±. -/
protected def mkOfNhds (n : Œ± ‚Üí Filter Œ±) : TopologicalSpace Œ± where
  IsOpen s := ‚àÄ a ‚àà s, s ‚àà n a
  isOpen_univ _ _ := univ_mem
  isOpen_inter := fun _s _t hs ht x ‚ü®hxs, hxt‚ü© => inter_mem (hs x hxs) (ht x hxt)
  isOpen_sUnion := fun _s hs _a ‚ü®x, hx, hxa‚ü© =>
    mem_of_superset (hs x hx _ hxa) (subset_sUnion_of_mem hx)

theorem nhds_mkOfNhds_of_hasBasis {n : Œ± ‚Üí Filter Œ±} {Œπ : Œ± ‚Üí Sort*} {p : ‚àÄ a, Œπ a ‚Üí Prop}
    {s : ‚àÄ a, Œπ a ‚Üí Set Œ±} (hb : ‚àÄ a, (n a).HasBasis (p a) (s a))
    (hpure : ‚àÄ a i, p a i ‚Üí a ‚àà s a i) (hopen : ‚àÄ a i, p a i ‚Üí ‚àÄ·∂† x in n a, s a i ‚àà n x) (a : Œ±) :
    @nhds Œ± (.mkOfNhds n) a = n a := by
  let t : TopologicalSpace Œ± := .mkOfNhds n
  apply le_antisymm
  ¬∑ intro U hU
    replace hpure : pure ‚â§ n := fun x ‚Ü¶ (hb x).ge_iff.2 (hpure x)
    refine mem_nhds_iff.2 ‚ü®{x | U ‚àà n x}, fun x hx ‚Ü¶ hpure x hx, fun x hx ‚Ü¶ ?_, hU‚ü©
    rcases (hb x).mem_iff.1 hx with ‚ü®i, hpi, hi‚ü©
    exact (hopen x i hpi).mono fun y hy ‚Ü¶ mem_of_superset hy hi
  ¬∑ exact (nhds_basis_opens a).ge_iff.2 fun U ‚ü®haU, hUo‚ü© ‚Ü¶ hUo a haU

theorem nhds_mkOfNhds (n : Œ± ‚Üí Filter Œ±) (a : Œ±) (h‚ÇÄ : pure ‚â§ n)
    (h‚ÇÅ : ‚àÄ a, ‚àÄ s ‚àà n a, ‚àÄ·∂† y in n a, s ‚àà n y) :
    @nhds Œ± (TopologicalSpace.mkOfNhds n) a = n a :=
  nhds_mkOfNhds_of_hasBasis (fun a ‚Ü¶ (n a).basis_sets) h‚ÇÄ h‚ÇÅ _

theorem nhds_mkOfNhds_single [DecidableEq Œ±] {a‚ÇÄ : Œ±} {l : Filter Œ±} (h : pure a‚ÇÄ ‚â§ l) (b : Œ±) :
    @nhds Œ± (TopologicalSpace.mkOfNhds (update pure a‚ÇÄ l)) b =
      (update pure a‚ÇÄ l : Œ± ‚Üí Filter Œ±) b := by
  refine nhds_mkOfNhds _ _ (le_update_iff.mpr ‚ü®h, fun _ _ => le_rfl‚ü©) fun a s hs => ?_
  rcases eq_or_ne a a‚ÇÄ with (rfl | ha)
  ¬∑ filter_upwards [hs] with b hb
    rcases eq_or_ne b a with (rfl | hb)
    ¬∑ exact hs
    ¬∑ rwa [update_of_ne hb]
  ¬∑ simpa only [update_of_ne ha, mem_pure, eventually_pure] using hs

theorem nhds_mkOfNhds_filterBasis (B : Œ± ‚Üí FilterBasis Œ±) (a : Œ±) (h‚ÇÄ : ‚àÄ x, ‚àÄ n ‚àà B x, x ‚àà n)
    (h‚ÇÅ : ‚àÄ x, ‚àÄ n ‚àà B x, ‚àÉ n‚ÇÅ ‚àà B x, ‚àÄ x' ‚àà n‚ÇÅ, ‚àÉ n‚ÇÇ ‚àà B x', n‚ÇÇ ‚äÜ n) :
    @nhds Œ± (TopologicalSpace.mkOfNhds fun x => (B x).filter) a = (B a).filter :=
  nhds_mkOfNhds_of_hasBasis (fun a ‚Ü¶ (B a).hasBasis) h‚ÇÄ h‚ÇÅ a

section Lattice

variable {Œ± : Type u} {Œ≤ : Type v}

/-- The ordering on topologies on the type `Œ±`. `t ‚â§ s` if every set open in `s` is also open in `t`
(`t` is finer than `s`). -/
instance : PartialOrder (TopologicalSpace Œ±) :=
  { PartialOrder.lift (fun t => OrderDual.toDual IsOpen[t]) (fun _ _ => TopologicalSpace.ext) with
    le := fun s t => ‚àÄ U, IsOpen[t] U ‚Üí IsOpen[s] U }

protected theorem le_def {Œ±} {t s : TopologicalSpace Œ±} : t ‚â§ s ‚Üî IsOpen[s] ‚â§ IsOpen[t] :=
  Iff.rfl

theorem le_generateFrom_iff_subset_isOpen {g : Set (Set Œ±)} {t : TopologicalSpace Œ±} :
    t ‚â§ generateFrom g ‚Üî g ‚äÜ { s | IsOpen[t] s } :=
  ‚ü®fun ht s hs => ht _ <| .basic s hs, fun hg _s hs =>
    hs.recOn (fun _ h => hg h) isOpen_univ (fun _ _ _ _ => IsOpen.inter) fun _ _ => isOpen_sUnion‚ü©

/-- If `s` equals the collection of open sets in the topology it generates, then `s` defines a
topology. -/
protected def mkOfClosure (s : Set (Set Œ±)) (hs : { u | GenerateOpen s u } = s) :
    TopologicalSpace Œ± where
  IsOpen u := u ‚àà s
  isOpen_univ := hs ‚ñ∏ TopologicalSpace.GenerateOpen.univ
  isOpen_inter := hs ‚ñ∏ TopologicalSpace.GenerateOpen.inter
  isOpen_sUnion := hs ‚ñ∏ TopologicalSpace.GenerateOpen.sUnion

theorem mkOfClosure_sets {s : Set (Set Œ±)} {hs : { u | GenerateOpen s u } = s} :
    TopologicalSpace.mkOfClosure s hs = generateFrom s :=
  TopologicalSpace.ext hs.symm

theorem gc_generateFrom (Œ±) :
    GaloisConnection (fun t : TopologicalSpace Œ± => OrderDual.toDual { s | IsOpen[t] s })
      (generateFrom ‚àò OrderDual.ofDual) := fun _ _ =>
  le_generateFrom_iff_subset_isOpen.symm

/-- The Galois coinsertion between `TopologicalSpace Œ±` and `(Set (Set Œ±))·µí·µà` whose lower part sends
  a topology to its collection of open subsets, and whose upper part sends a collection of subsets
  of `Œ±` to the topology they generate. -/
def gciGenerateFrom (Œ± : Type*) :
    GaloisCoinsertion (fun t : TopologicalSpace Œ± => OrderDual.toDual { s | IsOpen[t] s })
      (generateFrom ‚àò OrderDual.ofDual) where
  gc := gc_generateFrom Œ±
  u_l_le _ s hs := TopologicalSpace.GenerateOpen.basic s hs
  choice g hg := TopologicalSpace.mkOfClosure g
    (Subset.antisymm hg <| le_generateFrom_iff_subset_isOpen.1 <| le_rfl)
  choice_eq _ _ := mkOfClosure_sets

/-- Topologies on `Œ±` form a complete lattice, with `‚ä•` the discrete topology
  and `‚ä§` the indiscrete topology. The infimum of a collection of topologies
  is the topology generated by all their open sets, while the supremum is the
  topology whose open sets are those sets open in every member of the collection. -/
instance : CompleteLattice (TopologicalSpace Œ±) := (gciGenerateFrom Œ±).liftCompleteLattice

@[mono, gcongr]
theorem generateFrom_anti {Œ±} {g‚ÇÅ g‚ÇÇ : Set (Set Œ±)} (h : g‚ÇÅ ‚äÜ g‚ÇÇ) :
    generateFrom g‚ÇÇ ‚â§ generateFrom g‚ÇÅ :=
  (gc_generateFrom _).monotone_u h

theorem generateFrom_setOf_isOpen (t : TopologicalSpace Œ±) :
    generateFrom { s | IsOpen[t] s } = t :=
  (gciGenerateFrom Œ±).u_l_eq t

theorem leftInverse_generateFrom :
    LeftInverse generateFrom fun t : TopologicalSpace Œ± => { s | IsOpen[t] s } :=
  (gciGenerateFrom Œ±).u_l_leftInverse

theorem generateFrom_surjective : Surjective (generateFrom : Set (Set Œ±) ‚Üí TopologicalSpace Œ±) :=
  (gciGenerateFrom Œ±).u_surjective

theorem setOf_isOpen_injective : Injective fun t : TopologicalSpace Œ± => { s | IsOpen[t] s } :=
  (gciGenerateFrom Œ±).l_injective

end Lattice

end TopologicalSpace

section Lattice

variable {Œ± : Type*} {t t‚ÇÅ t‚ÇÇ : TopologicalSpace Œ±} {s : Set Œ±}

theorem IsOpen.mono (hs : IsOpen[t‚ÇÇ] s) (h : t‚ÇÅ ‚â§ t‚ÇÇ) : IsOpen[t‚ÇÅ] s := h s hs

theorem IsClosed.mono (hs : IsClosed[t‚ÇÇ] s) (h : t‚ÇÅ ‚â§ t‚ÇÇ) : IsClosed[t‚ÇÅ] s :=
  (@isOpen_compl_iff Œ± s t‚ÇÅ).mp <| hs.isOpen_compl.mono h

theorem closure.mono (h : t‚ÇÅ ‚â§ t‚ÇÇ) : closure[t‚ÇÅ] s ‚äÜ closure[t‚ÇÇ] s :=
  @closure_minimal _ t‚ÇÅ s (@closure _ t‚ÇÇ s) subset_closure (IsClosed.mono isClosed_closure h)

theorem isOpen_implies_isOpen_iff : (‚àÄ s, IsOpen[t‚ÇÅ] s ‚Üí IsOpen[t‚ÇÇ] s) ‚Üî t‚ÇÇ ‚â§ t‚ÇÅ :=
  Iff.rfl

/-- The only open sets in the indiscrete topology are the empty set and the whole space. -/
theorem TopologicalSpace.isOpen_top_iff {Œ±} (U : Set Œ±) : IsOpen[‚ä§] U ‚Üî U = ‚àÖ ‚à® U = univ :=
  ‚ü®fun h => by
    induction h with
    | basic _ h => exact False.elim h
    | univ => exact .inr rfl
    | inter _ _ _ _ h‚ÇÅ h‚ÇÇ =>
      rcases h‚ÇÅ with (rfl | rfl) <;> rcases h‚ÇÇ with (rfl | rfl) <;> simp
    | sUnion _ _ ih => exact sUnion_mem_empty_univ ih, by
      rintro (rfl | rfl)
      exacts [@isOpen_empty _ ‚ä§, @isOpen_univ _ ‚ä§]‚ü©

/-- A topological space is discrete if every set is open, that is,
  its topology equals the discrete topology `‚ä•`. -/
class DiscreteTopology (Œ± : Type*) [t : TopologicalSpace Œ±] : Prop where
  /-- The `TopologicalSpace` structure on a type with discrete topology is equal to `‚ä•`. -/
  eq_bot : t = ‚ä•

theorem discreteTopology_bot (Œ± : Type*) : @DiscreteTopology Œ± ‚ä• :=
  @DiscreteTopology.mk Œ± ‚ä• rfl

section DiscreteTopology

variable [TopologicalSpace Œ±] [DiscreteTopology Œ±] {Œ≤ : Type*}

@[simp]
theorem isOpen_discrete (s : Set Œ±) : IsOpen s := (@DiscreteTopology.eq_bot Œ± _).symm ‚ñ∏ trivial

@[simp] theorem isClosed_discrete (s : Set Œ±) : IsClosed s := ‚ü®isOpen_discrete _‚ü©

theorem closure_discrete (s : Set Œ±) : closure s = s := (isClosed_discrete _).closure_eq

@[simp] theorem dense_discrete {s : Set Œ±} : Dense s ‚Üî s = univ := by simp [dense_iff_closure_eq]

@[simp]
theorem denseRange_discrete {Œπ : Type*} {f : Œπ ‚Üí Œ±} : DenseRange f ‚Üî Surjective f := by
  rw [DenseRange, dense_discrete, range_eq_univ]

@[nontriviality, continuity, fun_prop]
theorem continuous_of_discreteTopology [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤} : Continuous f :=
  continuous_def.2 fun _ _ => isOpen_discrete _

/-- A function to a discrete topological space is continuous if and only if the preimage of every
singleton is open. -/
theorem continuous_discrete_rng {Œ±} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [DiscreteTopology Œ≤]
    {f : Œ± ‚Üí Œ≤} : Continuous f ‚Üî ‚àÄ b : Œ≤, IsOpen (f ‚Åª¬π' {b}) :=
  ‚ü®fun h _ => (isOpen_discrete _).preimage h, fun h => ‚ü®fun s _ => by
    rw [‚Üê biUnion_of_singleton s, preimage_iUnion‚ÇÇ]
    exact isOpen_biUnion fun _ _ => h _‚ü©‚ü©

@[simp]
theorem nhds_discrete (Œ± : Type*) [TopologicalSpace Œ±] [DiscreteTopology Œ±] : @nhds Œ± _ = pure :=
  le_antisymm (fun _ s hs => (isOpen_discrete s).mem_nhds hs) pure_le_nhds

theorem mem_nhds_discrete {x : Œ±} {s : Set Œ±} :
    s ‚àà ùìù x ‚Üî x ‚àà s := by rw [nhds_discrete, mem_pure]

end DiscreteTopology

theorem le_of_nhds_le_nhds (h : ‚àÄ x, @nhds Œ± t‚ÇÅ x ‚â§ @nhds Œ± t‚ÇÇ x) : t‚ÇÅ ‚â§ t‚ÇÇ := fun s => by
  rw [@isOpen_iff_mem_nhds _ t‚ÇÅ, @isOpen_iff_mem_nhds _ t‚ÇÇ]
  exact fun hs a ha => h _ (hs _ ha)

theorem eq_bot_of_singletons_open {t : TopologicalSpace Œ±} (h : ‚àÄ x, IsOpen[t] {x}) : t = ‚ä• :=
  bot_unique fun s _ => biUnion_of_singleton s ‚ñ∏ isOpen_biUnion fun x _ => h x

theorem forall_open_iff_discrete {X : Type*} [TopologicalSpace X] :
    (‚àÄ s : Set X, IsOpen s) ‚Üî DiscreteTopology X :=
  ‚ü®fun h => ‚ü®eq_bot_of_singletons_open fun _ => h _‚ü©, @isOpen_discrete _ _‚ü©

theorem discreteTopology_iff_forall_isClosed [TopologicalSpace Œ±] :
    DiscreteTopology Œ± ‚Üî ‚àÄ s : Set Œ±, IsClosed s :=
  forall_open_iff_discrete.symm.trans <| compl_surjective.forall.trans <| forall_congr' fun _ ‚Ü¶
    isOpen_compl_iff

theorem singletons_open_iff_discrete {X : Type*} [TopologicalSpace X] :
    (‚àÄ a : X, IsOpen ({a} : Set X)) ‚Üî DiscreteTopology X :=
  ‚ü®fun h => ‚ü®eq_bot_of_singletons_open h‚ü©, fun a _ => @isOpen_discrete _ _ a _‚ü©

theorem DiscreteTopology.of_finite_of_isClosed_singleton [TopologicalSpace Œ±] [Finite Œ±]
    (h : ‚àÄ a : Œ±, IsClosed {a}) : DiscreteTopology Œ± :=
  discreteTopology_iff_forall_isClosed.mpr fun s ‚Ü¶
    s.iUnion_of_singleton_coe ‚ñ∏ isClosed_iUnion_of_finite fun _ ‚Ü¶ h _

theorem discreteTopology_iff_singleton_mem_nhds [TopologicalSpace Œ±] :
    DiscreteTopology Œ± ‚Üî ‚àÄ x : Œ±, {x} ‚àà ùìù x := by
  simp only [‚Üê singletons_open_iff_discrete, isOpen_iff_mem_nhds, mem_singleton_iff, forall_eq]

/-- This lemma characterizes discrete topological spaces as those whose singletons are
neighbourhoods. -/
theorem discreteTopology_iff_nhds [TopologicalSpace Œ±] :
    DiscreteTopology Œ± ‚Üî ‚àÄ x : Œ±, ùìù x = pure x := by
  simp [discreteTopology_iff_singleton_mem_nhds]
  apply forall_congr' (fun x ‚Ü¶ ?_)
  simp [le_antisymm_iff, pure_le_nhds x]

theorem discreteTopology_iff_nhds_ne [TopologicalSpace Œ±] :
    DiscreteTopology Œ± ‚Üî ‚àÄ x : Œ±, ùìù[‚â†] x = ‚ä• := by
  simp only [discreteTopology_iff_singleton_mem_nhds, nhdsWithin, inf_principal_eq_bot, compl_compl]

/-- If the codomain of a continuous injective function has discrete topology,
then so does the domain.

See also `Embedding.discreteTopology` for an important special case. -/
theorem DiscreteTopology.of_continuous_injective
    {Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [DiscreteTopology Œ≤] {f : Œ± ‚Üí Œ≤}
    (hc : Continuous f) (hinj : Injective f) : DiscreteTopology Œ± :=
  forall_open_iff_discrete.1 fun s ‚Ü¶ hinj.preimage_image s ‚ñ∏ (isOpen_discrete _).preimage hc

end Lattice

section GaloisConnection

variable {Œ± Œ≤ Œ≥ : Type*}

theorem isOpen_induced_iff [t : TopologicalSpace Œ≤] {s : Set Œ±} {f : Œ± ‚Üí Œ≤} :
    IsOpen[t.induced f] s ‚Üî ‚àÉ t, IsOpen t ‚àß f ‚Åª¬π' t = s :=
  Iff.rfl

theorem isClosed_induced_iff [t : TopologicalSpace Œ≤] {s : Set Œ±} {f : Œ± ‚Üí Œ≤} :
    IsClosed[t.induced f] s ‚Üî ‚àÉ t, IsClosed t ‚àß f ‚Åª¬π' t = s := by
  letI := t.induced f
  simp only [‚Üê isOpen_compl_iff, isOpen_induced_iff]
  exact compl_surjective.exists.trans (by simp only [preimage_compl, compl_inj_iff])

theorem isOpen_coinduced {t : TopologicalSpace Œ±} {s : Set Œ≤} {f : Œ± ‚Üí Œ≤} :
    IsOpen[t.coinduced f] s ‚Üî IsOpen (f ‚Åª¬π' s) :=
  Iff.rfl

theorem isClosed_coinduced {t : TopologicalSpace Œ±} {s : Set Œ≤} {f : Œ± ‚Üí Œ≤} :
    IsClosed[t.coinduced f] s ‚Üî IsClosed (f ‚Åª¬π' s) := by
  simp only [‚Üê isOpen_compl_iff, isOpen_coinduced (f := f), preimage_compl]

theorem preimage_nhds_coinduced [TopologicalSpace Œ±] {œÄ : Œ± ‚Üí Œ≤} {s : Set Œ≤} {a : Œ±}
    (hs : s ‚àà @nhds Œ≤ (TopologicalSpace.coinduced œÄ ‚Äπ_‚Ä∫) (œÄ a)) : œÄ ‚Åª¬π' s ‚àà ùìù a := by
  letI := TopologicalSpace.coinduced œÄ ‚Äπ_‚Ä∫
  rcases mem_nhds_iff.mp hs with ‚ü®V, hVs, V_op, mem_V‚ü©
  exact mem_nhds_iff.mpr ‚ü®œÄ ‚Åª¬π' V, Set.preimage_mono hVs, V_op, mem_V‚ü©

variable {t t‚ÇÅ t‚ÇÇ : TopologicalSpace Œ±} {t' : TopologicalSpace Œ≤} {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ±}

theorem Continuous.coinduced_le (h : Continuous[t, t'] f) : t.coinduced f ‚â§ t' :=
  (@continuous_def Œ± Œ≤ t t').1 h

theorem coinduced_le_iff_le_induced {f : Œ± ‚Üí Œ≤} {tŒ± : TopologicalSpace Œ±}
    {tŒ≤ : TopologicalSpace Œ≤} : tŒ±.coinduced f ‚â§ tŒ≤ ‚Üî tŒ± ‚â§ tŒ≤.induced f :=
  ‚ü®fun h _s ‚ü®_t, ht, hst‚ü© => hst ‚ñ∏ h _ ht, fun h s hs => h _ ‚ü®s, hs, rfl‚ü©‚ü©

theorem Continuous.le_induced (h : Continuous[t, t'] f) : t ‚â§ t'.induced f :=
  coinduced_le_iff_le_induced.1 h.coinduced_le

theorem gc_coinduced_induced (f : Œ± ‚Üí Œ≤) :
    GaloisConnection (TopologicalSpace.coinduced f) (TopologicalSpace.induced f) := fun _ _ =>
  coinduced_le_iff_le_induced

theorem induced_mono (h : t‚ÇÅ ‚â§ t‚ÇÇ) : t‚ÇÅ.induced g ‚â§ t‚ÇÇ.induced g :=
  (gc_coinduced_induced g).monotone_u h

theorem coinduced_mono (h : t‚ÇÅ ‚â§ t‚ÇÇ) : t‚ÇÅ.coinduced f ‚â§ t‚ÇÇ.coinduced f :=
  (gc_coinduced_induced f).monotone_l h

@[simp]
theorem induced_top : (‚ä§ : TopologicalSpace Œ±).induced g = ‚ä§ :=
  (gc_coinduced_induced g).u_top

@[simp]
theorem induced_inf : (t‚ÇÅ ‚äì t‚ÇÇ).induced g = t‚ÇÅ.induced g ‚äì t‚ÇÇ.induced g :=
  (gc_coinduced_induced g).u_inf

@[simp]
theorem induced_iInf {Œπ : Sort w} {t : Œπ ‚Üí TopologicalSpace Œ±} :
    (‚®Ö i, t i).induced g = ‚®Ö i, (t i).induced g :=
  (gc_coinduced_induced g).u_iInf

@[simp]
theorem induced_sInf {s : Set (TopologicalSpace Œ±)} :
    TopologicalSpace.induced g (sInf s) = sInf (TopologicalSpace.induced g '' s) := by
  rw [sInf_eq_iInf', sInf_image', induced_iInf]

@[simp]
theorem coinduced_bot : (‚ä• : TopologicalSpace Œ±).coinduced f = ‚ä• :=
  (gc_coinduced_induced f).l_bot

@[simp]
theorem coinduced_sup : (t‚ÇÅ ‚äî t‚ÇÇ).coinduced f = t‚ÇÅ.coinduced f ‚äî t‚ÇÇ.coinduced f :=
  (gc_coinduced_induced f).l_sup

@[simp]
theorem coinduced_iSup {Œπ : Sort w} {t : Œπ ‚Üí TopologicalSpace Œ±} :
    (‚®Ü i, t i).coinduced f = ‚®Ü i, (t i).coinduced f :=
  (gc_coinduced_induced f).l_iSup

@[simp]
theorem coinduced_sSup {s : Set (TopologicalSpace Œ±)} :
    TopologicalSpace.coinduced f (sSup s) = sSup ((TopologicalSpace.coinduced f) '' s) := by
  rw [sSup_eq_iSup', sSup_image', coinduced_iSup]

theorem induced_id [t : TopologicalSpace Œ±] : t.induced id = t :=
  TopologicalSpace.ext <|
    funext fun s => propext <| ‚ü®fun ‚ü®_, hs, h‚ü© => h ‚ñ∏ hs, fun hs => ‚ü®s, hs, rfl‚ü©‚ü©

theorem induced_compose {tŒ≥ : TopologicalSpace Œ≥} {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ≥} :
    (tŒ≥.induced g).induced f = tŒ≥.induced (g ‚àò f) :=
  TopologicalSpace.ext <|
    funext fun _ => propext
      ‚ü®fun ‚ü®_, ‚ü®s, hs, h‚ÇÇ‚ü©, h‚ÇÅ‚ü© => h‚ÇÅ ‚ñ∏ h‚ÇÇ ‚ñ∏ ‚ü®s, hs, rfl‚ü©,
        fun ‚ü®s, hs, h‚ü© => ‚ü®preimage g s, ‚ü®s, hs, rfl‚ü©, h ‚ñ∏ rfl‚ü©‚ü©

theorem induced_const [t : TopologicalSpace Œ±] {x : Œ±} : (t.induced fun _ : Œ≤ => x) = ‚ä§ :=
  le_antisymm le_top (@continuous_const Œ≤ Œ± ‚ä§ t x).le_induced

theorem coinduced_id [t : TopologicalSpace Œ±] : t.coinduced id = t :=
  TopologicalSpace.ext rfl

theorem coinduced_compose [tŒ± : TopologicalSpace Œ±] {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ≥} :
    (tŒ±.coinduced f).coinduced g = tŒ±.coinduced (g ‚àò f) :=
  TopologicalSpace.ext rfl

theorem Equiv.induced_symm {Œ± Œ≤ : Type*} (e : Œ± ‚âÉ Œ≤) :
    TopologicalSpace.induced e.symm = TopologicalSpace.coinduced e := by
  ext t U
  rw [isOpen_induced_iff, isOpen_coinduced]
  simp only [e.symm.preimage_eq_iff_eq_image, exists_eq_right, ‚Üê preimage_equiv_eq_image_symm]

theorem Equiv.coinduced_symm {Œ± Œ≤ : Type*} (e : Œ± ‚âÉ Œ≤) :
    TopologicalSpace.coinduced e.symm = TopologicalSpace.induced e :=
  e.symm.induced_symm.symm

end GaloisConnection

-- constructions using the complete lattice structure
section Constructions

open TopologicalSpace

variable {Œ± : Type u} {Œ≤ : Type v}

instance inhabitedTopologicalSpace {Œ± : Type u} : Inhabited (TopologicalSpace Œ±) :=
  ‚ü®‚ä•‚ü©

instance (priority := 100) Subsingleton.uniqueTopologicalSpace [Subsingleton Œ±] :
    Unique (TopologicalSpace Œ±) where
  default := ‚ä•
  uniq t :=
    eq_bot_of_singletons_open fun x =>
      Subsingleton.set_cases (@isOpen_empty _ t) (@isOpen_univ _ t) ({x} : Set Œ±)

instance (priority := 100) Subsingleton.discreteTopology [t : TopologicalSpace Œ±] [Subsingleton Œ±] :
    DiscreteTopology Œ± :=
  ‚ü®Unique.eq_default t‚ü©

instance : TopologicalSpace Empty := ‚ä•
instance : DiscreteTopology Empty := ‚ü®rfl‚ü©

instance : TopologicalSpace PEmpty := ‚ä•
instance : DiscreteTopology PEmpty := ‚ü®rfl‚ü©

instance : TopologicalSpace PUnit := ‚ä•
instance : DiscreteTopology PUnit := ‚ü®rfl‚ü©

instance : TopologicalSpace Bool := ‚ä•
instance : DiscreteTopology Bool := ‚ü®rfl‚ü©

instance : TopologicalSpace ‚Ñï := ‚ä•
instance : DiscreteTopology ‚Ñï := ‚ü®rfl‚ü©

instance : TopologicalSpace ‚Ñ§ := ‚ä•
instance : DiscreteTopology ‚Ñ§ := ‚ü®rfl‚ü©

instance {n} : TopologicalSpace (Fin n) := ‚ä•
instance {n} : DiscreteTopology (Fin n) := ‚ü®rfl‚ü©

lemma Nat.cast_continuous {R : Type*} [NatCast R] [TopologicalSpace R] :
    Continuous (Nat.cast (R := R)) :=
  continuous_of_discreteTopology

lemma Int.cast_continuous {R : Type*} [IntCast R] [TopologicalSpace R] :
    Continuous (Int.cast (R := R)) :=
  continuous_of_discreteTopology

instance sierpinskiSpace : TopologicalSpace Prop :=
  generateFrom {{True}}

/-- See also `continuous_of_discreteTopology`, which works for `IsEmpty Œ±`. -/
theorem continuous_empty_function [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [IsEmpty Œ≤]
    (f : Œ± ‚Üí Œ≤) : Continuous f :=
  letI := Function.isEmpty f
  continuous_of_discreteTopology

theorem le_generateFrom {t : TopologicalSpace Œ±} {g : Set (Set Œ±)} (h : ‚àÄ s ‚àà g, IsOpen s) :
    t ‚â§ generateFrom g :=
  le_generateFrom_iff_subset_isOpen.2 h

theorem induced_generateFrom_eq {Œ± Œ≤} {b : Set (Set Œ≤)} {f : Œ± ‚Üí Œ≤} :
    (generateFrom b).induced f = generateFrom (preimage f '' b) :=
  le_antisymm (le_generateFrom <| forall_mem_image.2 fun s hs => ‚ü®s, GenerateOpen.basic _ hs, rfl‚ü©)
    (coinduced_le_iff_le_induced.1 <| le_generateFrom fun _s hs => .basic _ (mem_image_of_mem _ hs))

theorem le_induced_generateFrom {Œ± Œ≤} [t : TopologicalSpace Œ±] {b : Set (Set Œ≤)} {f : Œ± ‚Üí Œ≤}
    (h : ‚àÄ a : Set Œ≤, a ‚àà b ‚Üí IsOpen (f ‚Åª¬π' a)) : t ‚â§ induced f (generateFrom b) := by
  rw [induced_generateFrom_eq]
  apply le_generateFrom
  simp only [mem_image, and_imp, forall_apply_eq_imp_iff‚ÇÇ, exists_imp]
  exact h

lemma generateFrom_insert_of_generateOpen {Œ± : Type*} {s : Set (Set Œ±)} {t : Set Œ±}
    (ht : GenerateOpen s t) : generateFrom (insert t s) = generateFrom s := by
  refine le_antisymm (generateFrom_anti <| subset_insert t s) (le_generateFrom ?_)
  rintro t (rfl | h)
  ¬∑ exact ht
  ¬∑ exact isOpen_generateFrom_of_mem h

@[simp]
lemma generateFrom_insert_univ {Œ± : Type*} {s : Set (Set Œ±)} :
    generateFrom (insert univ s) = generateFrom s :=
  generateFrom_insert_of_generateOpen .univ

@[simp]
lemma generateFrom_insert_empty {Œ± : Type*} {s : Set (Set Œ±)} :
    generateFrom (insert ‚àÖ s) = generateFrom s := by
  rw [‚Üê sUnion_empty]
  exact generateFrom_insert_of_generateOpen (.sUnion ‚àÖ (fun s_1 a ‚Ü¶ False.elim a))

/-- This construction is left adjoint to the operation sending a topology on `Œ±`
  to its neighborhood filter at a fixed point `a : Œ±`. -/
def nhdsAdjoint (a : Œ±) (f : Filter Œ±) : TopologicalSpace Œ± where
  IsOpen s := a ‚àà s ‚Üí s ‚àà f
  isOpen_univ _ := univ_mem
  isOpen_inter := fun _s _t hs ht ‚ü®has, hat‚ü© => inter_mem (hs has) (ht hat)
  isOpen_sUnion := fun _k hk ‚ü®u, hu, hau‚ü© => mem_of_superset (hk u hu hau) (subset_sUnion_of_mem hu)

theorem gc_nhds (a : Œ±) : GaloisConnection (nhdsAdjoint a) fun t => @nhds Œ± t a := fun f t => by
  rw [le_nhds_iff]
  exact ‚ü®fun H s hs has => H _ has hs, fun H s has hs => H _ hs has‚ü©

theorem nhds_mono {t‚ÇÅ t‚ÇÇ : TopologicalSpace Œ±} {a : Œ±} (h : t‚ÇÅ ‚â§ t‚ÇÇ) :
    @nhds Œ± t‚ÇÅ a ‚â§ @nhds Œ± t‚ÇÇ a :=
  (gc_nhds a).monotone_u h

theorem le_iff_nhds {Œ± : Type*} (t t' : TopologicalSpace Œ±) :
    t ‚â§ t' ‚Üî ‚àÄ x, @nhds Œ± t x ‚â§ @nhds Œ± t' x :=
  ‚ü®fun h _ => nhds_mono h, le_of_nhds_le_nhds‚ü©

theorem isOpen_singleton_nhdsAdjoint {Œ± : Type*} {a b : Œ±} (f : Filter Œ±) (hb : b ‚â† a) :
    IsOpen[nhdsAdjoint a f] {b} := fun h ‚Ü¶
  absurd h hb.symm

theorem nhds_nhdsAdjoint_same (a : Œ±) (f : Filter Œ±) :
    @nhds Œ± (nhdsAdjoint a f) a = pure a ‚äî f := by
  let _ := nhdsAdjoint a f
  apply le_antisymm
  ¬∑ rintro t ‚ü®hat : a ‚àà t, htf : t ‚àà f‚ü©
    exact IsOpen.mem_nhds (fun _ ‚Ü¶ htf) hat
  ¬∑ exact sup_le (pure_le_nhds _) ((gc_nhds a).le_u_l f)

theorem nhds_nhdsAdjoint_of_ne {a b : Œ±} (f : Filter Œ±) (h : b ‚â† a) :
    @nhds Œ± (nhdsAdjoint a f) b = pure b :=
  let _ := nhdsAdjoint a f
  (isOpen_singleton_iff_nhds_eq_pure _).1 <| isOpen_singleton_nhdsAdjoint f h

theorem nhds_nhdsAdjoint [DecidableEq Œ±] (a : Œ±) (f : Filter Œ±) :
    @nhds Œ± (nhdsAdjoint a f) = update pure a (pure a ‚äî f) :=
  eq_update_iff.2 ‚ü®nhds_nhdsAdjoint_same .., fun _ ‚Ü¶ nhds_nhdsAdjoint_of_ne _‚ü©

theorem le_nhdsAdjoint_iff' {a : Œ±} {f : Filter Œ±} {t : TopologicalSpace Œ±} :
    t ‚â§ nhdsAdjoint a f ‚Üî @nhds Œ± t a ‚â§ pure a ‚äî f ‚àß ‚àÄ b ‚â† a, @nhds Œ± t b = pure b := by
  classical
  simp_rw [le_iff_nhds, nhds_nhdsAdjoint, forall_update_iff, (pure_le_nhds _).ge_iff_eq']

theorem le_nhdsAdjoint_iff {Œ± : Type*} (a : Œ±) (f : Filter Œ±) (t : TopologicalSpace Œ±) :
    t ‚â§ nhdsAdjoint a f ‚Üî @nhds Œ± t a ‚â§ pure a ‚äî f ‚àß ‚àÄ b ‚â† a, IsOpen[t] {b} := by
  simp only [le_nhdsAdjoint_iff', @isOpen_singleton_iff_nhds_eq_pure Œ± t]

theorem nhds_iInf {Œπ : Sort*} {t : Œπ ‚Üí TopologicalSpace Œ±} {a : Œ±} :
    @nhds Œ± (iInf t) a = ‚®Ö i, @nhds Œ± (t i) a :=
  (gc_nhds a).u_iInf

theorem nhds_sInf {s : Set (TopologicalSpace Œ±)} {a : Œ±} :
    @nhds Œ± (sInf s) a = ‚®Ö t ‚àà s, @nhds Œ± t a :=
  (gc_nhds a).u_sInf

-- Porting note (https://github.com/leanprover-community/mathlib4/issues/11215): TODO: timeouts without `b‚ÇÅ := t‚ÇÅ`
theorem nhds_inf {t‚ÇÅ t‚ÇÇ : TopologicalSpace Œ±} {a : Œ±} :
    @nhds Œ± (t‚ÇÅ ‚äì t‚ÇÇ) a = @nhds Œ± t‚ÇÅ a ‚äì @nhds Œ± t‚ÇÇ a :=
  (gc_nhds a).u_inf (b‚ÇÅ := t‚ÇÅ)

theorem nhds_top {a : Œ±} : @nhds Œ± ‚ä§ a = ‚ä§ :=
  (gc_nhds a).u_top

theorem isOpen_sup {t‚ÇÅ t‚ÇÇ : TopologicalSpace Œ±} {s : Set Œ±} :
    IsOpen[t‚ÇÅ ‚äî t‚ÇÇ] s ‚Üî IsOpen[t‚ÇÅ] s ‚àß IsOpen[t‚ÇÇ] s :=
  Iff.rfl

open TopologicalSpace

variable {Œ≥ : Type*} {f : Œ± ‚Üí Œ≤} {Œπ : Sort*}

theorem continuous_iff_coinduced_le {t‚ÇÅ : TopologicalSpace Œ±} {t‚ÇÇ : TopologicalSpace Œ≤} :
    Continuous[t‚ÇÅ, t‚ÇÇ] f ‚Üî coinduced f t‚ÇÅ ‚â§ t‚ÇÇ :=
  continuous_def

theorem continuous_iff_le_induced {t‚ÇÅ : TopologicalSpace Œ±} {t‚ÇÇ : TopologicalSpace Œ≤} :
    Continuous[t‚ÇÅ, t‚ÇÇ] f ‚Üî t‚ÇÅ ‚â§ induced f t‚ÇÇ :=
  Iff.trans continuous_iff_coinduced_le (gc_coinduced_induced f _ _)

lemma continuous_generateFrom_iff {t : TopologicalSpace Œ±} {b : Set (Set Œ≤)} :
    Continuous[t, generateFrom b] f ‚Üî ‚àÄ s ‚àà b, IsOpen (f ‚Åª¬π' s) := by
  rw [continuous_iff_coinduced_le, le_generateFrom_iff_subset_isOpen]
  simp only [isOpen_coinduced, preimage_id', subset_def, mem_setOf]

@[continuity, fun_prop]
theorem continuous_induced_dom {t : TopologicalSpace Œ≤} : Continuous[induced f t, t] f :=
  continuous_iff_le_induced.2 le_rfl

theorem continuous_induced_rng {g : Œ≥ ‚Üí Œ±} {t‚ÇÇ : TopologicalSpace Œ≤} {t‚ÇÅ : TopologicalSpace Œ≥} :
    Continuous[t‚ÇÅ, induced f t‚ÇÇ] g ‚Üî Continuous[t‚ÇÅ, t‚ÇÇ] (f ‚àò g) := by
  simp only [continuous_iff_le_induced, induced_compose]

theorem continuous_coinduced_rng {t : TopologicalSpace Œ±} :
    Continuous[t, coinduced f t] f :=
  continuous_iff_coinduced_le.2 le_rfl

theorem continuous_coinduced_dom {g : Œ≤ ‚Üí Œ≥} {t‚ÇÅ : TopologicalSpace Œ±} {t‚ÇÇ : TopologicalSpace Œ≥} :
    Continuous[coinduced f t‚ÇÅ, t‚ÇÇ] g ‚Üî Continuous[t‚ÇÅ, t‚ÇÇ] (g ‚àò f) := by
  simp only [continuous_iff_coinduced_le, coinduced_compose]

theorem continuous_le_dom {t‚ÇÅ t‚ÇÇ : TopologicalSpace Œ±} {t‚ÇÉ : TopologicalSpace Œ≤} (h‚ÇÅ : t‚ÇÇ ‚â§ t‚ÇÅ)
    (h‚ÇÇ : Continuous[t‚ÇÅ, t‚ÇÉ] f) : Continuous[t‚ÇÇ, t‚ÇÉ] f := by
  rw [continuous_iff_le_induced] at h‚ÇÇ ‚ä¢
  exact le_trans h‚ÇÅ h‚ÇÇ

theorem continuous_le_rng {t‚ÇÅ : TopologicalSpace Œ±} {t‚ÇÇ t‚ÇÉ : TopologicalSpace Œ≤} (h‚ÇÅ : t‚ÇÇ ‚â§ t‚ÇÉ)
    (h‚ÇÇ : Continuous[t‚ÇÅ, t‚ÇÇ] f) : Continuous[t‚ÇÅ, t‚ÇÉ] f := by
  rw [continuous_iff_coinduced_le] at h‚ÇÇ ‚ä¢
  exact le_trans h‚ÇÇ h‚ÇÅ

theorem continuous_sup_dom {t‚ÇÅ t‚ÇÇ : TopologicalSpace Œ±} {t‚ÇÉ : TopologicalSpace Œ≤} :
    Continuous[t‚ÇÅ ‚äî t‚ÇÇ, t‚ÇÉ] f ‚Üî Continuous[t‚ÇÅ, t‚ÇÉ] f ‚àß Continuous[t‚ÇÇ, t‚ÇÉ] f := by
  simp only [continuous_iff_le_induced, sup_le_iff]

theorem continuous_sup_rng_left {t‚ÇÅ : TopologicalSpace Œ±} {t‚ÇÉ t‚ÇÇ : TopologicalSpace Œ≤} :
    Continuous[t‚ÇÅ, t‚ÇÇ] f ‚Üí Continuous[t‚ÇÅ, t‚ÇÇ ‚äî t‚ÇÉ] f :=
  continuous_le_rng le_sup_left

theorem continuous_sup_rng_right {t‚ÇÅ : TopologicalSpace Œ±} {t‚ÇÉ t‚ÇÇ : TopologicalSpace Œ≤} :
    Continuous[t‚ÇÅ, t‚ÇÉ] f ‚Üí Continuous[t‚ÇÅ, t‚ÇÇ ‚äî t‚ÇÉ] f :=
  continuous_le_rng le_sup_right

theorem continuous_sSup_dom {T : Set (TopologicalSpace Œ±)} {t‚ÇÇ : TopologicalSpace Œ≤} :
    Continuous[sSup T, t‚ÇÇ] f ‚Üî ‚àÄ t ‚àà T, Continuous[t, t‚ÇÇ] f := by
  simp only [continuous_iff_le_induced, sSup_le_iff]

theorem continuous_sSup_rng {t‚ÇÅ : TopologicalSpace Œ±} {t‚ÇÇ : Set (TopologicalSpace Œ≤)}
    {t : TopologicalSpace Œ≤} (h‚ÇÅ : t ‚àà t‚ÇÇ) (hf : Continuous[t‚ÇÅ, t] f) :
    Continuous[t‚ÇÅ, sSup t‚ÇÇ] f :=
  continuous_iff_coinduced_le.2 <| le_sSup_of_le h‚ÇÅ <| continuous_iff_coinduced_le.1 hf

theorem continuous_iSup_dom {t‚ÇÅ : Œπ ‚Üí TopologicalSpace Œ±} {t‚ÇÇ : TopologicalSpace Œ≤} :
    Continuous[iSup t‚ÇÅ, t‚ÇÇ] f ‚Üî ‚àÄ i, Continuous[t‚ÇÅ i, t‚ÇÇ] f := by
  simp only [continuous_iff_le_induced, iSup_le_iff]

theorem continuous_iSup_rng {t‚ÇÅ : TopologicalSpace Œ±} {t‚ÇÇ : Œπ ‚Üí TopologicalSpace Œ≤} {i : Œπ}
    (h : Continuous[t‚ÇÅ, t‚ÇÇ i] f) : Continuous[t‚ÇÅ, iSup t‚ÇÇ] f :=
  continuous_sSup_rng ‚ü®i, rfl‚ü© h

theorem continuous_inf_rng {t‚ÇÅ : TopologicalSpace Œ±} {t‚ÇÇ t‚ÇÉ : TopologicalSpace Œ≤} :
    Continuous[t‚ÇÅ, t‚ÇÇ ‚äì t‚ÇÉ] f ‚Üî Continuous[t‚ÇÅ, t‚ÇÇ] f ‚àß Continuous[t‚ÇÅ, t‚ÇÉ] f := by
  simp only [continuous_iff_coinduced_le, le_inf_iff]

theorem continuous_inf_dom_left {t‚ÇÅ t‚ÇÇ : TopologicalSpace Œ±} {t‚ÇÉ : TopologicalSpace Œ≤} :
    Continuous[t‚ÇÅ, t‚ÇÉ] f ‚Üí Continuous[t‚ÇÅ ‚äì t‚ÇÇ, t‚ÇÉ] f :=
  continuous_le_dom inf_le_left

theorem continuous_inf_dom_right {t‚ÇÅ t‚ÇÇ : TopologicalSpace Œ±} {t‚ÇÉ : TopologicalSpace Œ≤} :
    Continuous[t‚ÇÇ, t‚ÇÉ] f ‚Üí Continuous[t‚ÇÅ ‚äì t‚ÇÇ, t‚ÇÉ] f :=
  continuous_le_dom inf_le_right

theorem continuous_sInf_dom {t‚ÇÅ : Set (TopologicalSpace Œ±)} {t‚ÇÇ : TopologicalSpace Œ≤}
    {t : TopologicalSpace Œ±} (h‚ÇÅ : t ‚àà t‚ÇÅ) :
    Continuous[t, t‚ÇÇ] f ‚Üí Continuous[sInf t‚ÇÅ, t‚ÇÇ] f :=
  continuous_le_dom <| sInf_le h‚ÇÅ

theorem continuous_sInf_rng {t‚ÇÅ : TopologicalSpace Œ±} {T : Set (TopologicalSpace Œ≤)} :
    Continuous[t‚ÇÅ, sInf T] f ‚Üî ‚àÄ t ‚àà T, Continuous[t‚ÇÅ, t] f := by
  simp only [continuous_iff_coinduced_le, le_sInf_iff]

theorem continuous_iInf_dom {t‚ÇÅ : Œπ ‚Üí TopologicalSpace Œ±} {t‚ÇÇ : TopologicalSpace Œ≤} {i : Œπ} :
    Continuous[t‚ÇÅ i, t‚ÇÇ] f ‚Üí Continuous[iInf t‚ÇÅ, t‚ÇÇ] f :=
  continuous_le_dom <| iInf_le _ _

theorem continuous_iInf_rng {t‚ÇÅ : TopologicalSpace Œ±} {t‚ÇÇ : Œπ ‚Üí TopologicalSpace Œ≤} :
    Continuous[t‚ÇÅ, iInf t‚ÇÇ] f ‚Üî ‚àÄ i, Continuous[t‚ÇÅ, t‚ÇÇ i] f := by
  simp only [continuous_iff_coinduced_le, le_iInf_iff]

@[continuity, fun_prop]
theorem continuous_bot {t : TopologicalSpace Œ≤} : Continuous[‚ä•, t] f :=
  continuous_iff_le_induced.2 bot_le

@[continuity, fun_prop]
theorem continuous_top {t : TopologicalSpace Œ±} : Continuous[t, ‚ä§] f :=
  continuous_iff_coinduced_le.2 le_top

theorem continuous_id_iff_le {t t' : TopologicalSpace Œ±} : Continuous[t, t'] id ‚Üî t ‚â§ t' :=
  @continuous_def _ _ t t' id

theorem continuous_id_of_le {t t' : TopologicalSpace Œ±} (h : t ‚â§ t') : Continuous[t, t'] id :=
  continuous_id_iff_le.2 h

-- ùìù in the induced topology
theorem mem_nhds_induced [T : TopologicalSpace Œ±] (f : Œ≤ ‚Üí Œ±) (a : Œ≤) (s : Set Œ≤) :
    s ‚àà @nhds Œ≤ (TopologicalSpace.induced f T) a ‚Üî ‚àÉ u ‚àà ùìù (f a), f ‚Åª¬π' u ‚äÜ s := by
  letI := T.induced f
  simp_rw [mem_nhds_iff, isOpen_induced_iff]
  constructor
  ¬∑ rintro ‚ü®u, usub, ‚ü®v, openv, rfl‚ü©, au‚ü©
    exact ‚ü®v, ‚ü®v, Subset.rfl, openv, au‚ü©, usub‚ü©
  ¬∑ rintro ‚ü®u, ‚ü®v, vsubu, openv, amem‚ü©, finvsub‚ü©
    exact ‚ü®f ‚Åª¬π' v, (Set.preimage_mono vsubu).trans finvsub, ‚ü®‚ü®v, openv, rfl‚ü©, amem‚ü©‚ü©

theorem nhds_induced [T : TopologicalSpace Œ±] (f : Œ≤ ‚Üí Œ±) (a : Œ≤) :
    @nhds Œ≤ (TopologicalSpace.induced f T) a = comap f (ùìù (f a)) := by
  ext s
  rw [mem_nhds_induced, mem_comap]

theorem induced_iff_nhds_eq [tŒ± : TopologicalSpace Œ±] [tŒ≤ : TopologicalSpace Œ≤] (f : Œ≤ ‚Üí Œ±) :
    tŒ≤ = tŒ±.induced f ‚Üî ‚àÄ b, ùìù b = comap f (ùìù <| f b) := by
  simp only [ext_iff_nhds, nhds_induced]

theorem map_nhds_induced_of_surjective [T : TopologicalSpace Œ±] {f : Œ≤ ‚Üí Œ±} (hf : Surjective f)
    (a : Œ≤) : map f (@nhds Œ≤ (TopologicalSpace.induced f T) a) = ùìù (f a) := by
  rw [nhds_induced, map_comap_of_surjective hf]

theorem continuous_nhdsAdjoint_dom [TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤} {a : Œ±} {l : Filter Œ±} :
    Continuous[nhdsAdjoint a l, _] f ‚Üî Tendsto f l (ùìù (f a)) := by
  simp_rw [continuous_iff_le_induced, gc_nhds _ _, nhds_induced, tendsto_iff_comap]

theorem coinduced_nhdsAdjoint (f : Œ± ‚Üí Œ≤) (a : Œ±) (l : Filter Œ±) :
    coinduced f (nhdsAdjoint a l) = nhdsAdjoint (f a) (map f l) :=
  eq_of_forall_ge_iff fun _ ‚Ü¶ by
    rw [gc_nhds, ‚Üê continuous_iff_coinduced_le, continuous_nhdsAdjoint_dom, Tendsto]

end Constructions

section Induced

open TopologicalSpace

variable {Œ± : Type*} {Œ≤ : Type*}
variable [t : TopologicalSpace Œ≤] {f : Œ± ‚Üí Œ≤}

theorem isOpen_induced_eq {s : Set Œ±} :
    IsOpen[induced f t] s ‚Üî s ‚àà preimage f '' { s | IsOpen s } :=
  Iff.rfl

theorem isOpen_induced {s : Set Œ≤} (h : IsOpen s) : IsOpen[induced f t] (f ‚Åª¬π' s) :=
  ‚ü®s, h, rfl‚ü©

theorem map_nhds_induced_eq (a : Œ±) : map f (@nhds Œ± (induced f t) a) = ùìù[range f] f a := by
  rw [nhds_induced, Filter.map_comap, nhdsWithin]

theorem map_nhds_induced_of_mem {a : Œ±} (h : range f ‚àà ùìù (f a)) :
    map f (@nhds Œ± (induced f t) a) = ùìù (f a) := by rw [nhds_induced, Filter.map_comap_of_mem h]

theorem closure_induced {f : Œ± ‚Üí Œ≤} {a : Œ±} {s : Set Œ±} :
    a ‚àà @closure Œ± (t.induced f) s ‚Üî f a ‚àà closure (f '' s) := by
  letI := t.induced f
  simp only [mem_closure_iff_frequently, nhds_induced, frequently_comap, mem_image, and_comm]

theorem isClosed_induced_iff' {f : Œ± ‚Üí Œ≤} {s : Set Œ±} :
    IsClosed[t.induced f] s ‚Üî ‚àÄ a, f a ‚àà closure (f '' s) ‚Üí a ‚àà s := by
  letI := t.induced f
  simp only [‚Üê closure_subset_iff_isClosed, subset_def, closure_induced]

end Induced

section Sierpinski

variable {Œ± : Type*}

@[simp]
theorem isOpen_singleton_true : IsOpen ({True} : Set Prop) :=
  TopologicalSpace.GenerateOpen.basic _ (mem_singleton _)

@[simp]
theorem nhds_true : ùìù True = pure True :=
  le_antisymm (le_pure_iff.2 <| isOpen_singleton_true.mem_nhds <| mem_singleton _) (pure_le_nhds _)

@[simp]
theorem nhds_false : ùìù False = ‚ä§ :=
  TopologicalSpace.nhds_generateFrom.trans <| by simp [@and_comm (_ ‚àà _)]

theorem tendsto_nhds_true {l : Filter Œ±} {p : Œ± ‚Üí Prop} :
    Tendsto p l (ùìù True) ‚Üî ‚àÄ·∂† x in l, p x := by simp

theorem tendsto_nhds_Prop {l : Filter Œ±} {p : Œ± ‚Üí Prop} {q : Prop} :
    Tendsto p l (ùìù q) ‚Üî (q ‚Üí ‚àÄ·∂† x in l, p x) := by
  by_cases q <;> simp [*]

variable [TopologicalSpace Œ±]

theorem continuous_Prop {p : Œ± ‚Üí Prop} : Continuous p ‚Üî IsOpen { x | p x } := by
  simp only [continuous_iff_continuousAt, ContinuousAt, tendsto_nhds_Prop, isOpen_iff_mem_nhds]; rfl

theorem isOpen_iff_continuous_mem {s : Set Œ±} : IsOpen s ‚Üî Continuous (¬∑ ‚àà s) :=
  continuous_Prop.symm

end Sierpinski

section iInf

open TopologicalSpace

variable {Œ± : Type u} {Œπ : Sort v}

theorem generateFrom_union (a‚ÇÅ a‚ÇÇ : Set (Set Œ±)) :
    generateFrom (a‚ÇÅ ‚à™ a‚ÇÇ) = generateFrom a‚ÇÅ ‚äì generateFrom a‚ÇÇ :=
  (gc_generateFrom Œ±).u_inf

theorem setOf_isOpen_sup (t‚ÇÅ t‚ÇÇ : TopologicalSpace Œ±) :
    { s | IsOpen[t‚ÇÅ ‚äî t‚ÇÇ] s } = { s | IsOpen[t‚ÇÅ] s } ‚à© { s | IsOpen[t‚ÇÇ] s } :=
  rfl

theorem generateFrom_iUnion {f : Œπ ‚Üí Set (Set Œ±)} :
    generateFrom (‚ãÉ i, f i) = ‚®Ö i, generateFrom (f i) :=
  (gc_generateFrom Œ±).u_iInf

theorem setOf_isOpen_iSup {t : Œπ ‚Üí TopologicalSpace Œ±} :
    { s | IsOpen[‚®Ü i, t i] s } = ‚ãÇ i, { s | IsOpen[t i] s } :=
  (gc_generateFrom Œ±).l_iSup

theorem generateFrom_sUnion {S : Set (Set (Set Œ±))} :
    generateFrom (‚ãÉ‚ÇÄ S) = ‚®Ö s ‚àà S, generateFrom s :=
  (gc_generateFrom Œ±).u_sInf

theorem setOf_isOpen_sSup {T : Set (TopologicalSpace Œ±)} :
    { s | IsOpen[sSup T] s } = ‚ãÇ t ‚àà T, { s | IsOpen[t] s } :=
  (gc_generateFrom Œ±).l_sSup

theorem generateFrom_union_isOpen (a b : TopologicalSpace Œ±) :
    generateFrom ({ s | IsOpen[a] s } ‚à™ { s | IsOpen[b] s }) = a ‚äì b :=
  (gciGenerateFrom Œ±).u_inf_l _ _

theorem generateFrom_iUnion_isOpen (f : Œπ ‚Üí TopologicalSpace Œ±) :
    generateFrom (‚ãÉ i, { s | IsOpen[f i] s }) = ‚®Ö i, f i :=
  (gciGenerateFrom Œ±).u_iInf_l _

theorem generateFrom_inter (a b : TopologicalSpace Œ±) :
    generateFrom ({ s | IsOpen[a] s } ‚à© { s | IsOpen[b] s }) = a ‚äî b :=
  (gciGenerateFrom Œ±).u_sup_l _ _

theorem generateFrom_iInter (f : Œπ ‚Üí TopologicalSpace Œ±) :
    generateFrom (‚ãÇ i, { s | IsOpen[f i] s }) = ‚®Ü i, f i :=
  (gciGenerateFrom Œ±).u_iSup_l _

theorem generateFrom_iInter_of_generateFrom_eq_self (f : Œπ ‚Üí Set (Set Œ±))
    (hf : ‚àÄ i, { s | IsOpen[generateFrom (f i)] s } = f i) :
    generateFrom (‚ãÇ i, f i) = ‚®Ü i, generateFrom (f i) :=
  (gciGenerateFrom Œ±).u_iSup_of_lu_eq_self f hf

variable {t : Œπ ‚Üí TopologicalSpace Œ±}

theorem isOpen_iSup_iff {s : Set Œ±} : IsOpen[‚®Ü i, t i] s ‚Üî ‚àÄ i, IsOpen[t i] s :=
  show s ‚àà {s | IsOpen[iSup t] s} ‚Üî s ‚àà { x : Set Œ± | ‚àÄ i : Œπ, IsOpen[t i] x } by
    simp [setOf_isOpen_iSup]

theorem isOpen_sSup_iff {s : Set Œ±} {T : Set (TopologicalSpace Œ±)} :
    IsOpen[sSup T] s ‚Üî ‚àÄ t ‚àà T, IsOpen[t] s := by
  simp only [sSup_eq_iSup, isOpen_iSup_iff]

theorem isClosed_iSup_iff {s : Set Œ±} : IsClosed[‚®Ü i, t i] s ‚Üî ‚àÄ i, IsClosed[t i] s := by
  simp only [‚Üê @isOpen_compl_iff _ _ (‚®Ü i, t i), ‚Üê @isOpen_compl_iff _ _ (t _), isOpen_iSup_iff]

theorem isClosed_sSup_iff {s : Set Œ±} {T : Set (TopologicalSpace Œ±)} :
    IsClosed[sSup T] s ‚Üî ‚àÄ t ‚àà T, IsClosed[t] s := by
  simp only [sSup_eq_iSup, isClosed_iSup_iff]

end iInf
