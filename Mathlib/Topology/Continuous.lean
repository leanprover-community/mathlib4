/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Jeremy Avigad
-/
import Mathlib.Topology.ClusterPt

/-!
# Continuity in topological spaces

For topological spaces `X` and `Y`, a function `f : X â†’ Y` and a point `x : X`,
`ContinuousAt f x` means `f` is continuous at `x`, and global continuity is
`Continuous f`. There is also a version of continuity `PContinuous` for
partially defined functions.

## Tags

continuity, continuous function
-/

open Set Filter Topology

variable {X Y Z : Type*}

open TopologicalSpace

-- The curly braces are intentional, so this definition works well with simp
-- when topologies are not those provided by instances.
theorem continuous_def {_ : TopologicalSpace X} {_ : TopologicalSpace Y} {f : X â†’ Y} :
    Continuous f â†” âˆ€ s, IsOpen s â†’ IsOpen (f â»Â¹' s) :=
  âŸ¨fun hf => hf.1, fun h => âŸ¨hâŸ©âŸ©

variable [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]
variable {f : X â†’ Y} {s : Set X} {x : X} {y : Y}

theorem IsOpen.preimage (hf : Continuous f) {t : Set Y} (h : IsOpen t) :
    IsOpen (f â»Â¹' t) :=
  hf.isOpen_preimage t h

lemma Equiv.continuous_symm_iff (e : X â‰ƒ Y) : Continuous e.symm â†” IsOpenMap e := by
  simp_rw [continuous_def, â† Set.image_equiv_eq_preimage_symm, IsOpenMap]

lemma Equiv.isOpenMap_symm_iff (e : X â‰ƒ Y) : IsOpenMap e.symm â†” Continuous e := by
  simp_rw [â† Equiv.continuous_symm_iff, Equiv.symm_symm]

theorem continuous_congr {g : X â†’ Y} (h : âˆ€ x, f x = g x) :
    Continuous f â†” Continuous g :=
  .of_eq <| congrArg _ <| funext h

theorem Continuous.congr {g : X â†’ Y} (h : Continuous f) (h' : âˆ€ x, f x = g x) : Continuous g :=
  continuous_congr h' |>.mp h

theorem ContinuousAt.tendsto (h : ContinuousAt f x) :
    Tendsto f (ğ“ x) (ğ“ (f x)) :=
  h

theorem continuousAt_def : ContinuousAt f x â†” âˆ€ A âˆˆ ğ“ (f x), f â»Â¹' A âˆˆ ğ“ x :=
  Iff.rfl

theorem continuousAt_congr {g : X â†’ Y} (h : f =á¶ [ğ“ x] g) :
    ContinuousAt f x â†” ContinuousAt g x := by
  simp only [ContinuousAt, tendsto_congr' h, h.eq_of_nhds]

theorem ContinuousAt.congr {g : X â†’ Y} (hf : ContinuousAt f x) (h : f =á¶ [ğ“ x] g) :
    ContinuousAt g x :=
  (continuousAt_congr h).1 hf

theorem ContinuousAt.preimage_mem_nhds {t : Set Y} (h : ContinuousAt f x)
    (ht : t âˆˆ ğ“ (f x)) : f â»Â¹' t âˆˆ ğ“ x :=
  h ht

/-- If `f x âˆˆ s âˆˆ ğ“ (f x)` for continuous `f`, then `f y âˆˆ s` near `x`.

This is essentially `Filter.Tendsto.eventually_mem`, but infers in more cases when applied. -/
theorem ContinuousAt.eventually_mem {f : X â†’ Y} {x : X} (hf : ContinuousAt f x) {s : Set Y}
    (hs : s âˆˆ ğ“ (f x)) : âˆ€á¶  y in ğ“ x, f y âˆˆ s :=
  hf hs

/-- If a function `f` tends to somewhere other than `ğ“ (f x)` at `x`,
then `f` is not continuous at `x`
-/
lemma not_continuousAt_of_tendsto {f : X â†’ Y} {lâ‚ : Filter X} {lâ‚‚ : Filter Y} {x : X}
    (hf : Tendsto f lâ‚ lâ‚‚) [lâ‚.NeBot] (hlâ‚ : lâ‚ â‰¤ ğ“ x) (hlâ‚‚ : Disjoint (ğ“ (f x)) lâ‚‚) :
    Â¬ ContinuousAt f x := fun cont â†¦
  (cont.mono_left hlâ‚).not_tendsto hlâ‚‚ hf

theorem ClusterPt.map {lx : Filter X} {ly : Filter Y} (H : ClusterPt x lx)
    (hfc : ContinuousAt f x) (hf : Tendsto f lx ly) : ClusterPt (f x) ly :=
  (NeBot.map H f).mono <| hfc.tendsto.inf hf

/-- See also `interior_preimage_subset_preimage_interior`. -/
theorem preimage_interior_subset_interior_preimage {t : Set Y} (hf : Continuous f) :
    f â»Â¹' interior t âŠ† interior (f â»Â¹' t) :=
  interior_maximal (preimage_mono interior_subset) (isOpen_interior.preimage hf)

@[continuity]
theorem continuous_id : Continuous (id : X â†’ X) :=
  continuous_def.2 fun _ => id

-- This is needed due to reducibility issues with the `continuity` tactic.
@[continuity, fun_prop]
theorem continuous_id' : Continuous (fun (x : X) => x) := continuous_id

theorem Continuous.comp {g : Y â†’ Z} (hg : Continuous g) (hf : Continuous f) :
    Continuous (g âˆ˜ f) :=
  continuous_def.2 fun _ h => (h.preimage hg).preimage hf

-- This is needed due to reducibility issues with the `continuity` tactic.
@[continuity, fun_prop]
theorem Continuous.comp' {g : Y â†’ Z} (hg : Continuous g) (hf : Continuous f) :
    Continuous (fun x => g (f x)) := hg.comp hf

theorem Continuous.iterate {f : X â†’ X} (h : Continuous f) (n : â„•) : Continuous f^[n] :=
  Nat.recOn n continuous_id fun _ ihn => ihn.comp h

nonrec theorem ContinuousAt.comp {g : Y â†’ Z} (hg : ContinuousAt g (f x))
    (hf : ContinuousAt f x) : ContinuousAt (g âˆ˜ f) x :=
  hg.comp hf

@[fun_prop]
theorem ContinuousAt.comp' {g : Y â†’ Z} {x : X} (hg : ContinuousAt g (f x))
    (hf : ContinuousAt f x) : ContinuousAt (fun x => g (f x)) x := ContinuousAt.comp hg hf

/-- See note [comp_of_eq lemmas] -/
theorem ContinuousAt.comp_of_eq {g : Y â†’ Z} (hg : ContinuousAt g y)
    (hf : ContinuousAt f x) (hy : f x = y) : ContinuousAt (g âˆ˜ f) x := by subst hy; exact hg.comp hf

theorem Continuous.tendsto (hf : Continuous f) (x) : Tendsto f (ğ“ x) (ğ“ (f x)) :=
  ((nhds_basis_opens x).tendsto_iff <| nhds_basis_opens <| f x).2 fun t âŸ¨hxt, htâŸ© =>
    âŸ¨f â»Â¹' t, âŸ¨hxt, ht.preimage hfâŸ©, Subset.rflâŸ©

/-- A version of `Continuous.tendsto` that allows one to specify a simpler form of the limit.
E.g., one can write `continuous_exp.tendsto' 0 1 exp_zero`. -/
theorem Continuous.tendsto' (hf : Continuous f) (x : X) (y : Y) (h : f x = y) :
    Tendsto f (ğ“ x) (ğ“ y) :=
  h â–¸ hf.tendsto x

@[fun_prop]
theorem Continuous.continuousAt (h : Continuous f) : ContinuousAt f x :=
  h.tendsto x

theorem continuous_iff_continuousAt : Continuous f â†” âˆ€ x, ContinuousAt f x :=
  âŸ¨Continuous.tendsto, fun hf => continuous_def.2 fun _U hU => isOpen_iff_mem_nhds.2 fun x hx =>
    hf x <| hU.mem_nhds hxâŸ©

@[fun_prop]
theorem continuousAt_const : ContinuousAt (fun _ : X => y) x :=
  tendsto_const_nhds

@[continuity, fun_prop]
theorem continuous_const : Continuous fun _ : X => y :=
  continuous_iff_continuousAt.mpr fun _ => continuousAt_const

theorem Filter.EventuallyEq.continuousAt (h : f =á¶ [ğ“ x] fun _ => y) :
    ContinuousAt f x :=
  (continuousAt_congr h).2 tendsto_const_nhds

theorem continuous_of_const (h : âˆ€ x y, f x = f y) : Continuous f :=
  continuous_iff_continuousAt.mpr fun x =>
    Filter.EventuallyEq.continuousAt <| Eventually.of_forall fun y => h y x

theorem continuousAt_id : ContinuousAt id x :=
  continuous_id.continuousAt

@[fun_prop]
theorem continuousAt_id' (y) : ContinuousAt (fun x : X => x) y := continuousAt_id

theorem ContinuousAt.iterate {f : X â†’ X} (hf : ContinuousAt f x) (hx : f x = x) (n : â„•) :
    ContinuousAt f^[n] x :=
  Nat.recOn n continuousAt_id fun _n ihn â†¦ ihn.comp_of_eq hf hx

theorem continuous_iff_isClosed : Continuous f â†” âˆ€ s, IsClosed s â†’ IsClosed (f â»Â¹' s) :=
  continuous_def.trans <| compl_surjective.forall.trans <| by
    simp only [isOpen_compl_iff, preimage_compl]

theorem IsClosed.preimage (hf : Continuous f) {t : Set Y} (h : IsClosed t) :
    IsClosed (f â»Â¹' t) :=
  continuous_iff_isClosed.mp hf t h

theorem mem_closure_image (hf : ContinuousAt f x)
    (hx : x âˆˆ closure s) : f x âˆˆ closure (f '' s) :=
  mem_closure_of_frequently_of_tendsto
    ((mem_closure_iff_frequently.1 hx).mono fun _ => mem_image_of_mem _) hf

theorem Continuous.closure_preimage_subset (hf : Continuous f) (t : Set Y) :
    closure (f â»Â¹' t) âŠ† f â»Â¹' closure t := by
  rw [â† (isClosed_closure.preimage hf).closure_eq]
  exact closure_mono (preimage_mono subset_closure)

theorem Continuous.frontier_preimage_subset (hf : Continuous f) (t : Set Y) :
    frontier (f â»Â¹' t) âŠ† f â»Â¹' frontier t :=
  diff_subset_diff (hf.closure_preimage_subset t) (preimage_interior_subset_interior_preimage hf)

/-- If a continuous map `f` maps `s` to `t`, then it maps `closure s` to `closure t`. -/
protected theorem Set.MapsTo.closure {t : Set Y} (h : MapsTo f s t)
    (hc : Continuous f) : MapsTo f (closure s) (closure t) := by
  simp only [MapsTo, mem_closure_iff_clusterPt]
  exact fun x hx => hx.map hc.continuousAt (tendsto_principal_principal.2 h)

/-- See also `IsClosedMap.closure_image_eq_of_continuous`. -/
theorem image_closure_subset_closure_image (h : Continuous f) :
    f '' closure s âŠ† closure (f '' s) :=
  ((mapsTo_image f s).closure h).image_subset

theorem closure_image_closure (h : Continuous f) :
    closure (f '' closure s) = closure (f '' s) :=
  Subset.antisymm
    (closure_minimal (image_closure_subset_closure_image h) isClosed_closure)
    (closure_mono <| image_subset _ subset_closure)

theorem closure_subset_preimage_closure_image (h : Continuous f) :
    closure s âŠ† f â»Â¹' closure (f '' s) :=
  (mapsTo_image _ _).closure h

theorem map_mem_closure {t : Set Y} (hf : Continuous f)
    (hx : x âˆˆ closure s) (ht : MapsTo f s t) : f x âˆˆ closure t :=
  ht.closure hf hx

/-- If a continuous map `f` maps `s` to a closed set `t`, then it maps `closure s` to `t`. -/
theorem Set.MapsTo.closure_left {t : Set Y} (h : MapsTo f s t)
    (hc : Continuous f) (ht : IsClosed t) : MapsTo f (closure s) t :=
  ht.closure_eq â–¸ h.closure hc

theorem Filter.Tendsto.lift'_closure (hf : Continuous f) {l l'} (h : Tendsto f l l') :
    Tendsto f (l.lift' closure) (l'.lift' closure) :=
  tendsto_lift'.2 fun s hs â†¦ by
    filter_upwards [mem_lift' (h hs)] using (mapsTo_preimage _ _).closure hf

theorem tendsto_lift'_closure_nhds (hf : Continuous f) (x : X) :
    Tendsto f ((ğ“ x).lift' closure) ((ğ“ (f x)).lift' closure) :=
  (hf.tendsto x).lift'_closure hf

/-!
### Function with dense range
-/

section DenseRange

variable {Î± Î¹ : Type*} (f : Î± â†’ X) (g : X â†’ Y)
variable {f : Î± â†’ X} {s : Set X}

/-- A surjective map has dense range. -/
theorem Function.Surjective.denseRange (hf : Function.Surjective f) : DenseRange f := fun x => by
  simp [hf.range_eq]

theorem denseRange_id : DenseRange (id : X â†’ X) :=
  Function.Surjective.denseRange Function.surjective_id

theorem denseRange_iff_closure_range : DenseRange f â†” closure (range f) = univ :=
  dense_iff_closure_eq

theorem DenseRange.closure_range (h : DenseRange f) : closure (range f) = univ :=
  h.closure_eq

@[simp]
lemma denseRange_subtype_val {p : X â†’ Prop} : DenseRange (@Subtype.val _ p) â†” Dense {x | p x} := by
  simp [DenseRange]

theorem Dense.denseRange_val (h : Dense s) : DenseRange ((â†‘) : s â†’ X) :=
  denseRange_subtype_val.2 h

theorem Continuous.range_subset_closure_image_dense {f : X â†’ Y} (hf : Continuous f)
    (hs : Dense s) : range f âŠ† closure (f '' s) := by
  rw [â† image_univ, â† hs.closure_eq]
  exact image_closure_subset_closure_image hf

/-- The image of a dense set under a continuous map with dense range is a dense set. -/
theorem DenseRange.dense_image {f : X â†’ Y} (hf' : DenseRange f) (hf : Continuous f)
    (hs : Dense s) : Dense (f '' s) :=
  (hf'.mono <| hf.range_subset_closure_image_dense hs).of_closure

/-- If `f` has dense range and `s` is an open set in the codomain of `f`, then the image of the
preimage of `s` under `f` is dense in `s`. -/
theorem DenseRange.subset_closure_image_preimage_of_isOpen (hf : DenseRange f) (hs : IsOpen s) :
    s âŠ† closure (f '' (f â»Â¹' s)) := by
  rw [image_preimage_eq_inter_range]
  exact hf.open_subset_closure_inter hs

/-- If a continuous map with dense range maps a dense set to a subset of `t`, then `t` is a dense
set. -/
theorem DenseRange.dense_of_mapsTo {f : X â†’ Y} (hf' : DenseRange f) (hf : Continuous f)
    (hs : Dense s) {t : Set Y} (ht : MapsTo f s t) : Dense t :=
  (hf'.dense_image hf hs).mono ht.image_subset

/-- Composition of a continuous map with dense range and a function with dense range has dense
range. -/
theorem DenseRange.comp {g : Y â†’ Z} {f : Î± â†’ Y} (hg : DenseRange g) (hf : DenseRange f)
    (cg : Continuous g) : DenseRange (g âˆ˜ f) := by
  rw [DenseRange, range_comp]
  exact hg.dense_image cg hf

nonrec theorem DenseRange.nonempty_iff (hf : DenseRange f) : Nonempty Î± â†” Nonempty X :=
  range_nonempty_iff_nonempty.symm.trans hf.nonempty_iff

theorem DenseRange.nonempty [h : Nonempty X] (hf : DenseRange f) : Nonempty Î± :=
  hf.nonempty_iff.mpr h

/-- Given a function `f : X â†’ Y` with dense range and `y : Y`, returns some `x : X`. -/
noncomputable def DenseRange.some (hf : DenseRange f) (x : X) : Î± :=
  Classical.choice <| hf.nonempty_iff.mpr âŸ¨xâŸ©

nonrec theorem DenseRange.exists_mem_open (hf : DenseRange f) (ho : IsOpen s) (hs : s.Nonempty) :
    âˆƒ a, f a âˆˆ s :=
  exists_range_iff.1 <| hf.exists_mem_open ho hs

theorem DenseRange.mem_nhds (h : DenseRange f) (hs : s âˆˆ ğ“ x) :
    âˆƒ a, f a âˆˆ s :=
  let âŸ¨a, haâŸ© := h.exists_mem_open isOpen_interior âŸ¨x, mem_interior_iff_mem_nhds.2 hsâŸ©
  âŸ¨a, interior_subset haâŸ©

end DenseRange

library_note "continuity lemma statement"/--
The library contains many lemmas stating that functions/operations are continuous. There are many
ways to formulate the continuity of operations. Some are more convenient than others.
Note: for the most part this note also applies to other properties
(`Measurable`, `Differentiable`, `ContinuousOn`, ...).

### The traditional way
As an example, let's look at addition `(+) : M â†’ M â†’ M`. We can state that this is continuous
in different definitionally equal ways (omitting some typing information)
* `Continuous (fun p â†¦ p.1 + p.2)`;
* `Continuous (Function.uncurry (+))`;
* `Continuous â†¿(+)`. (`â†¿` is notation for recursively uncurrying a function)

However, lemmas with this conclusion are not nice to use in practice because
1. They confuse the elaborator. The following two examples fail, because of limitations in the
  elaboration process.
  ```
  variable {M : Type*} [Add M] [TopologicalSpace M] [ContinuousAdd M]
  example : Continuous (fun x : M â†¦ x + x) :=
    continuous_add.comp _

  example : Continuous (fun x : M â†¦ x + x) :=
    continuous_add.comp (continuous_id.prodMk continuous_id)
  ```
  The second is a valid proof, which is accepted if you write it as
  `continuous_add.comp (continuous_id.prodMk continuous_id :)`

2. If the operation has more than 2 arguments, they are impractical to use, because in your
  application the arguments in the domain might be in a different order or associated differently.

### The convenient way

A much more convenient way to write continuity lemmas is like `Continuous.add`:
```
Continuous.add {f g : X â†’ M} (hf : Continuous f) (hg : Continuous g) :
  Continuous (fun x â†¦ f x + g x)
```
The conclusion can be `Continuous (f + g)`, which is definitionally equal.
This has the following advantages
* It supports projection notation, so is shorter to write.
* `Continuous.add _ _` is recognized correctly by the elaborator and gives useful new goals.
* It works generally, since the domain is a variable.

As an example for a unary operation, we have `Continuous.neg`.
```
Continuous.neg {f : X â†’ G} (hf : Continuous f) : Continuous (fun x â†¦ -f x)
```
For unary functions, the elaborator is not confused when applying the traditional lemma
(like `continuous_neg`), but it's still convenient to have the short version available (compare
`hf.neg.neg.neg` with `continuous_neg.comp <| continuous_neg.comp <| continuous_neg.comp hf`).

As a harder example, consider an operation of the following type:
```
def strans {x : F} (Î³ Î³' : Path x x) (tâ‚€ : I) : Path x x
```
The precise definition is not important, only its type.
The correct continuity principle for this operation is something like this:
```
{f : X â†’ F} {Î³ Î³' : âˆ€ x, Path (f x) (f x)} {tâ‚€ s : X â†’ I}
  (hÎ³ : Continuous â†¿Î³) (hÎ³' : Continuous â†¿Î³')
  (ht : Continuous tâ‚€) (hs : Continuous s) :
  Continuous (fun x â†¦ strans (Î³ x) (Î³' x) (t x) (s x))
```
Note that *all* arguments of `strans` are indexed over `X`, even the basepoint `x`, and the last
argument `s` that arises since `Path x x` has a coercion to `I â†’ F`. The paths `Î³` and `Î³'` (which
are unary functions from `I`) become binary functions in the continuity lemma.

### Summary
* Make sure that your continuity lemmas are stated in the most general way, and in a convenient
  form. That means that:
  - The conclusion has a variable `X` as domain (not something like `Y Ã— Z`);
  - Wherever possible, all point arguments `c : Y` are replaced by functions `c : X â†’ Y`;
  - All `n`-ary function arguments are replaced by `n+1`-ary functions
    (`f : Y â†’ Z` becomes `f : X â†’ Y â†’ Z`);
  - All (relevant) arguments have continuity assumptions, and perhaps there are additional
    assumptions needed to make the operation continuous;
  - The function in the conclusion is fully applied.
* These remarks are mostly about the format of the *conclusion* of a continuity lemma.
  In assumptions it's fine to state that a function with more than 1 argument is continuous using
  `â†¿` or `Function.uncurry`.

### Functions with discontinuities

In some cases, you want to work with discontinuous functions, and in certain expressions they are
still continuous. For example, consider the fractional part of a number, `Int.fract : â„ â†’ â„`.
In this case, you want to add conditions to when a function involving `fract` is continuous, so you
get something like this: (assumption `hf` could be weakened, but the important thing is the shape
of the conclusion)
```
lemma ContinuousOn.comp_fract {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    {f : X â†’ â„ â†’ Y} {g : X â†’ â„} (hf : Continuous â†¿f) (hg : Continuous g) (h : âˆ€ s, f s 0 = f s 1) :
    Continuous (fun x â†¦ f x (fract (g x)))
```
With `ContinuousAt` you can be even more precise about what to prove in case of discontinuities,
see e.g. `ContinuousAt.comp_div_cases`.
-/

library_note "comp_of_eq lemmas"/--
Lean's elaborator has trouble elaborating applications of lemmas that state that the composition of
two functions satisfy some property at a point, like `ContinuousAt.comp` / `ContDiffAt.comp` and
`ContMDiffWithinAt.comp`. The reason is that a lemma like this looks like
`ContinuousAt g (f x) â†’ ContinuousAt f x â†’ ContinuousAt (g âˆ˜ f) x`.
Since Lean's elaborator elaborates the arguments from left-to-right, when you write `hg.comp hf`,
the elaborator will try to figure out *both* `f` and `g` from the type of `hg`. It tries to figure
out `f` just from the point where `g` is continuous. For example, if `hg : ContinuousAt g (a, x)`
then the elaborator will assign `f` to the function `Prod.mk a`, since in that case `f x = (a, x)`.
This is undesirable in most cases where `f` is not a variable. There are some ways to work around
this, for example by giving `f` explicitly, or to force Lean to elaborate `hf` before elaborating
`hg`, but this is annoying.
Another better solution is to reformulate composition lemmas to have the following shape
`ContinuousAt g y â†’ ContinuousAt f x â†’ f x = y â†’ ContinuousAt (g âˆ˜ f) x`.
This is even useful if the proof of `f x = y` is `rfl`.
The reason that this works better is because the type of `hg` doesn't mention `f`.
Only after elaborating the two `ContinuousAt` arguments, Lean will try to unify `f x` with `y`,
which is often easy after having chosen the correct functions for `f` and `g`.
Here is an example that shows the difference:
```
example [TopologicalSpace X] [TopologicalSpace Y] {xâ‚€ : X} (f : X â†’ X â†’ Y)
    (hf : ContinuousAt (Function.uncurry f) (xâ‚€, xâ‚€)) :
    ContinuousAt (fun x â†¦ f x x) xâ‚€ :=
  -- hf.comp (continuousAt_id.prod continuousAt_id) -- type mismatch
  -- hf.comp_of_eq (continuousAt_id.prod continuousAt_id) rfl -- works
```
-/
