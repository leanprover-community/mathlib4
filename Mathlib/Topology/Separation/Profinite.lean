/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro
-/

import Mathlib.Topology.Separation.Regular
import Mathlib.Topology.Connected.TotallyDisconnected

/-!
# Separation properties: profinite spaces
-/

open Function Set Filter Topology TopologicalSpace

universe u v

variable {X : Type*} {Y : Type*} [TopologicalSpace X]

section Profinite

/-- A T0 space with a clopen basis is totally separated. -/
theorem totallySeparatedSpace_of_t0_of_basis_clopen [T0Space X]
    (h : IsTopologicalBasis { s : Set X | IsClopen s }) : TotallySeparatedSpace X := by
  constructor
  rintro x - y - hxy
  choose U hU using exists_isOpen_xor'_mem hxy
  obtain âŸ¨hUâ‚€, hUâ‚âŸ© := hU
  rcases hUâ‚ with hx | hy
  Â· choose V hV using h.isOpen_iff.mp hUâ‚€ x hx.1
    exact âŸ¨V, Vá¶œ, hV.1.isOpen, hV.1.compl.isOpen, hV.2.1, notMem_subset hV.2.2 hx.2,
      (union_compl_self V).superset, disjoint_compl_rightâŸ©
  Â· choose V hV using h.isOpen_iff.mp hUâ‚€ y hy.1
    exact âŸ¨Vá¶œ, V, hV.1.compl.isOpen, hV.1.isOpen, notMem_subset hV.2.2 hy.2, hV.2.1,
      (union_comm _ _ â–¸ union_compl_self V).superset, disjoint_compl_leftâŸ©

@[deprecated (since := "2025-09-11")]
alias totallySeparatedSpace_of_t1_of_basis_clopen := totallySeparatedSpace_of_t0_of_basis_clopen

variable [T2Space X] [CompactSpace X] [TotallyDisconnectedSpace X]

theorem nhds_basis_clopen (x : X) : (ğ“ x).HasBasis (fun s : Set X => x âˆˆ s âˆ§ IsClopen s) id :=
  âŸ¨fun U => by
    constructor
    Â· have hx : connectedComponent x = {x} :=
        totallyDisconnectedSpace_iff_connectedComponent_singleton.mp â€¹_â€º x
      rw [connectedComponent_eq_iInter_isClopen] at hx
      intro hU
      let N := { s // IsClopen s âˆ§ x âˆˆ s }
      rsuffices âŸ¨âŸ¨s, hs, hs'âŸ©, hs''âŸ© : âˆƒ s : N, s.val âŠ† U
      Â· exact âŸ¨s, âŸ¨hs', hsâŸ©, hs''âŸ©
      haveI : Nonempty N := âŸ¨âŸ¨univ, isClopen_univ, mem_univ xâŸ©âŸ©
      have hNcl : âˆ€ s : N, IsClosed s.val := fun s => s.property.1.1
      have hdir : Directed Superset fun s : N => s.val := by
        rintro âŸ¨s, hs, hxsâŸ© âŸ¨t, ht, hxtâŸ©
        exact âŸ¨âŸ¨s âˆ© t, hs.inter ht, âŸ¨hxs, hxtâŸ©âŸ©, inter_subset_left, inter_subset_rightâŸ©
      have h_nhds : âˆ€ y âˆˆ â‹‚ s : N, s.val, U âˆˆ ğ“ y := fun y y_in => by
        rw [hx, mem_singleton_iff] at y_in
        rwa [y_in]
      exact exists_subset_nhds_of_compactSpace hdir hNcl h_nhds
    Â· rintro âŸ¨V, âŸ¨hxV, -, V_opâŸ©, hUV : V âŠ† UâŸ©
      rw [mem_nhds_iff]
      exact âŸ¨V, hUV, V_op, hxVâŸ©âŸ©

theorem isTopologicalBasis_isClopen : IsTopologicalBasis { s : Set X | IsClopen s } := by
  apply isTopologicalBasis_of_isOpen_of_nhds fun U (hU : IsClopen U) => hU.2
  intro x U hxU U_op
  have : U âˆˆ ğ“ x := IsOpen.mem_nhds U_op hxU
  rcases (nhds_basis_clopen x).mem_iff.mp this with âŸ¨V, âŸ¨hxV, hVâŸ©, hVU : V âŠ† UâŸ©
  use V
  tauto

/-- Every member of an open set in a compact Hausdorff totally disconnected space
  is contained in a clopen set contained in the open set. -/
theorem compact_exists_isClopen_in_isOpen {x : X} {U : Set X} (is_open : IsOpen U) (memU : x âˆˆ U) :
    âˆƒ V : Set X, IsClopen V âˆ§ x âˆˆ V âˆ§ V âŠ† U :=
  isTopologicalBasis_isClopen.mem_nhds_iff.1 (is_open.mem_nhds memU)

end Profinite

section LocallyCompact

variable {H : Type*} [TopologicalSpace H] [LocallyCompactSpace H] [T2Space H]

/-- A locally compact Hausdorff totally disconnected space has a basis with clopen elements. -/
theorem loc_compact_Haus_tot_disc_of_zero_dim [TotallyDisconnectedSpace H] :
    IsTopologicalBasis { s : Set H | IsClopen s } := by
  refine isTopologicalBasis_of_isOpen_of_nhds (fun u hu => hu.2) fun x U memU hU => ?_
  obtain âŸ¨s, comp, xs, sUâŸ© := exists_compact_subset hU memU
  let u : Set s := ((â†‘) : s â†’ H) â»Â¹' interior s
  have u_open_in_s : IsOpen u := isOpen_interior.preimage continuous_subtype_val
  lift x to s using interior_subset xs
  haveI : CompactSpace s := isCompact_iff_compactSpace.1 comp
  obtain âŸ¨V : Set s, VisClopen, Vx, V_subâŸ© := compact_exists_isClopen_in_isOpen u_open_in_s xs
  have VisClopen' : IsClopen (((â†‘) : s â†’ H) '' V) := by
    refine âŸ¨comp.isClosed.isClosedEmbedding_subtypeVal.isClosed_iff_image_isClosed.1 VisClopen.1,
      ?_âŸ©
    let v : Set u := ((â†‘) : u â†’ s) â»Â¹' V
    have : ((â†‘) : u â†’ H) = ((â†‘) : s â†’ H) âˆ˜ ((â†‘) : u â†’ s) := rfl
    have f0 : IsEmbedding ((â†‘) : u â†’ H) := IsEmbedding.subtypeVal.comp IsEmbedding.subtypeVal
    have f1 : IsOpenEmbedding ((â†‘) : u â†’ H) := by
      refine âŸ¨f0, ?_âŸ©
      Â· have : Set.range ((â†‘) : u â†’ H) = interior s := by
          rw [this, Set.range_comp, Subtype.range_coe, Subtype.image_preimage_coe]
          apply Set.inter_eq_self_of_subset_right interior_subset
        rw [this]
        apply isOpen_interior
    have f2 : IsOpen v := VisClopen.2.preimage continuous_subtype_val
    have f3 : ((â†‘) : s â†’ H) '' V = ((â†‘) : u â†’ H) '' v := by
      rw [this, image_comp, Subtype.image_preimage_coe, inter_eq_self_of_subset_right V_sub]
    rw [f3]
    apply f1.isOpenMap v f2
  use (â†‘) '' V, VisClopen', by simp [Vx], Subset.trans (by simp) sU

/-- A locally compact Hausdorff space is totally disconnected
  if and only if it is totally separated. -/
theorem loc_compact_t2_tot_disc_iff_tot_sep :
    TotallyDisconnectedSpace H â†” TotallySeparatedSpace H := by
  constructor
  Â· intro h
    exact totallySeparatedSpace_of_t0_of_basis_clopen loc_compact_Haus_tot_disc_of_zero_dim
  apply TotallySeparatedSpace.totallyDisconnectedSpace

/-- A totally disconnected compact Hausdorff space is totally separated. -/
instance (priority := 100) [TotallyDisconnectedSpace H] : TotallySeparatedSpace H :=
  loc_compact_t2_tot_disc_iff_tot_sep.mp inferInstance

end LocallyCompact
