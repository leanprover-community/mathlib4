/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro
-/
module

public import Mathlib.Data.Fintype.Option
public import Mathlib.Topology.Separation.Regular
public import Mathlib.Topology.Connected.TotallyDisconnected

/-!
# Separation properties: profinite spaces
-/

@[expose] public section

open Function Set Filter Topology TopologicalSpace

universe u v

variable {X : Type*} {Y : Type*} [TopologicalSpace X]

section Profinite

/-- A T0 space with a clopen basis is totally separated. -/
theorem totallySeparatedSpace_of_t0_of_basis_clopen [T0Space X]
    (h : IsTopologicalBasis { s : Set X | IsClopen s }) : TotallySeparatedSpace X := by
  constructor
  rintro x - y - hxy
  choose U hU using exists_isOpen_xor'_mem hxy
  obtain ‚ü®hU‚ÇÄ, hU‚ÇÅ‚ü© := hU
  rcases hU‚ÇÅ with hx | hy
  ¬∑ choose V hV using h.isOpen_iff.mp hU‚ÇÄ x hx.1
    exact ‚ü®V, V·∂ú, hV.1.isOpen, hV.1.compl.isOpen, hV.2.1, notMem_subset hV.2.2 hx.2,
      (union_compl_self V).superset, disjoint_compl_right‚ü©
  ¬∑ choose V hV using h.isOpen_iff.mp hU‚ÇÄ y hy.1
    exact ‚ü®V·∂ú, V, hV.1.compl.isOpen, hV.1.isOpen, notMem_subset hV.2.2 hy.2, hV.2.1,
      (union_comm _ _ ‚ñ∏ union_compl_self V).superset, disjoint_compl_left‚ü©

@[deprecated (since := "2025-09-11")]
alias totallySeparatedSpace_of_t1_of_basis_clopen := totallySeparatedSpace_of_t0_of_basis_clopen

variable [T2Space X] [CompactSpace X] [TotallyDisconnectedSpace X]

theorem nhds_basis_clopen (x : X) : (ùìù x).HasBasis (fun s : Set X => x ‚àà s ‚àß IsClopen s) id :=
  ‚ü®fun U => by
    constructor
    ¬∑ have hx : connectedComponent x = {x} :=
        totallyDisconnectedSpace_iff_connectedComponent_singleton.mp ‚Äπ_‚Ä∫ x
      rw [connectedComponent_eq_iInter_isClopen] at hx
      intro hU
      let N := { s // IsClopen s ‚àß x ‚àà s }
      rsuffices ‚ü®‚ü®s, hs, hs'‚ü©, hs''‚ü© : ‚àÉ s : N, s.val ‚äÜ U
      ¬∑ exact ‚ü®s, ‚ü®hs', hs‚ü©, hs''‚ü©
      haveI : Nonempty N := ‚ü®‚ü®univ, isClopen_univ, mem_univ x‚ü©‚ü©
      have hNcl : ‚àÄ s : N, IsClosed s.val := fun s => s.property.1.1
      have hdir : Directed Superset fun s : N => s.val := by
        rintro ‚ü®s, hs, hxs‚ü© ‚ü®t, ht, hxt‚ü©
        exact ‚ü®‚ü®s ‚à© t, hs.inter ht, ‚ü®hxs, hxt‚ü©‚ü©, inter_subset_left, inter_subset_right‚ü©
      have h_nhds : ‚àÄ y ‚àà ‚ãÇ s : N, s.val, U ‚àà ùìù y := fun y y_in => by
        rw [hx, mem_singleton_iff] at y_in
        rwa [y_in]
      exact exists_subset_nhds_of_compactSpace hdir hNcl h_nhds
    ¬∑ rintro ‚ü®V, ‚ü®hxV, -, V_op‚ü©, hUV : V ‚äÜ U‚ü©
      rw [mem_nhds_iff]
      exact ‚ü®V, hUV, V_op, hxV‚ü©‚ü©

theorem isTopologicalBasis_isClopen : IsTopologicalBasis { s : Set X | IsClopen s } := by
  apply isTopologicalBasis_of_isOpen_of_nhds fun U (hU : IsClopen U) => hU.2
  intro x U hxU U_op
  have : U ‚àà ùìù x := IsOpen.mem_nhds U_op hxU
  rcases (nhds_basis_clopen x).mem_iff.mp this with ‚ü®V, ‚ü®hxV, hV‚ü©, hVU : V ‚äÜ U‚ü©
  use V
  tauto

/-- Every member of an open set in a compact Hausdorff totally disconnected space
  is contained in a clopen set contained in the open set. -/
theorem compact_exists_isClopen_in_isOpen {x : X} {U : Set X} (is_open : IsOpen U) (memU : x ‚àà U) :
    ‚àÉ V : Set X, IsClopen V ‚àß x ‚àà V ‚àß V ‚äÜ U :=
  isTopologicalBasis_isClopen.mem_nhds_iff.1 (is_open.mem_nhds memU)

end Profinite

section LocallyCompact

variable {H : Type*} [TopologicalSpace H] [LocallyCompactSpace H] [T2Space H]

/-- A locally compact Hausdorff totally disconnected space has a basis with clopen elements. -/
theorem loc_compact_Haus_tot_disc_of_zero_dim [TotallyDisconnectedSpace H] :
    IsTopologicalBasis { s : Set H | IsClopen s } := by
  refine isTopologicalBasis_of_isOpen_of_nhds (fun u hu => hu.2) fun x U memU hU => ?_
  obtain ‚ü®s, comp, xs, sU‚ü© := exists_compact_subset hU memU
  let u : Set s := ((‚Üë) : s ‚Üí H) ‚Åª¬π' interior s
  have u_open_in_s : IsOpen u := isOpen_interior.preimage continuous_subtype_val
  lift x to s using interior_subset xs
  haveI : CompactSpace s := isCompact_iff_compactSpace.1 comp
  obtain ‚ü®V : Set s, VisClopen, Vx, V_sub‚ü© := compact_exists_isClopen_in_isOpen u_open_in_s xs
  have VisClopen' : IsClopen (((‚Üë) : s ‚Üí H) '' V) := by
    refine ‚ü®comp.isClosed.isClosedEmbedding_subtypeVal.isClosed_iff_image_isClosed.1 VisClopen.1,
      ?_‚ü©
    let v : Set u := ((‚Üë) : u ‚Üí s) ‚Åª¬π' V
    have : ((‚Üë) : u ‚Üí H) = ((‚Üë) : s ‚Üí H) ‚àò ((‚Üë) : u ‚Üí s) := rfl
    have f0 : IsEmbedding ((‚Üë) : u ‚Üí H) := IsEmbedding.subtypeVal.comp IsEmbedding.subtypeVal
    have f1 : IsOpenEmbedding ((‚Üë) : u ‚Üí H) := by
      refine ‚ü®f0, ?_‚ü©
      ¬∑ have : Set.range ((‚Üë) : u ‚Üí H) = interior s := by
          rw [this, Set.range_comp, Subtype.range_coe, Subtype.image_preimage_coe]
          apply Set.inter_eq_self_of_subset_right interior_subset
        rw [this]
        apply isOpen_interior
    have f2 : IsOpen v := VisClopen.2.preimage continuous_subtype_val
    have f3 : ((‚Üë) : s ‚Üí H) '' V = ((‚Üë) : u ‚Üí H) '' v := by
      rw [this, image_comp, Subtype.image_preimage_coe, inter_eq_self_of_subset_right V_sub]
    rw [f3]
    apply f1.isOpenMap v f2
  use (‚Üë) '' V, VisClopen', by simp [Vx], Subset.trans (by simp) sU

/-- A locally compact Hausdorff space is totally disconnected
  if and only if it is totally separated. -/
theorem loc_compact_t2_tot_disc_iff_tot_sep :
    TotallyDisconnectedSpace H ‚Üî TotallySeparatedSpace H := by
  constructor
  ¬∑ intro h
    exact totallySeparatedSpace_of_t0_of_basis_clopen loc_compact_Haus_tot_disc_of_zero_dim
  apply TotallySeparatedSpace.totallyDisconnectedSpace

/-- A totally disconnected compact Hausdorff space is totally separated. -/
instance (priority := 100) [TotallyDisconnectedSpace H] : TotallySeparatedSpace H :=
  loc_compact_t2_tot_disc_iff_tot_sep.mp inferInstance

/-- In a totally disconnected compact Hausdorff space `X`, if `Z ‚äÜ U` are subsets with `Z` closed
and `U` open, there exists a clopen `C` with `Z ‚äÜ C ‚äÜ U`. -/
lemma exists_clopen_of_closed_subset_open {X : Type*}
    [TopologicalSpace X] [CompactSpace X] [T2Space X] [TotallyDisconnectedSpace X]
    {Z U : Set X} (hZ : IsClosed Z) (hU : IsOpen U) (hZU : Z ‚äÜ U) :
    ‚àÉ C : Set X, IsClopen C ‚àß Z ‚äÜ C ‚àß C ‚äÜ U := by
  -- every `z ‚àà Z` has clopen neighborhood `V z ‚äÜ U`
  choose V hV using fun (z : Z) ‚Ü¶ compact_exists_isClopen_in_isOpen hU (hZU z.property)
  -- the `V z` cover `Z`
  have V_cover : Z ‚äÜ ‚ãÉ z, V z := fun z hz ‚Ü¶ mem_iUnion.mpr ‚ü®‚ü®z, hz‚ü©, (hV ‚ü®z, hz‚ü©).2.1‚ü©
  -- choose a finite subcover
  choose I hI using hZ.isCompact.elim_finite_subcover V (fun z ‚Ü¶ (hV z).1.isOpen) V_cover
  -- the union of this finite subcover does the job
  exact ‚ü®‚ãÉ (i ‚àà I), V i, I.finite_toSet.isClopen_biUnion (fun i _ ‚Ü¶ (hV i).1), hI, by simp_all‚ü©

/-- Let `X` be a totally disconnected compact Hausdorff space, `D i ‚äÜ X` a finite family of clopens,
and `Z i ‚äÜ D i` closed. Assume that the `Z i` are pairwise disjoint. Then there exist clopens
`Z i ‚äÜ C i ‚äÜ D i` with the `C i` disjoint, and such that `‚à™ D i ‚äÜ ‚à™ C i`. -/
lemma exists_clopen_partition_of_clopen_cover
    {X I : Type*} [TopologicalSpace X] [CompactSpace X] [T2Space X] [TotallyDisconnectedSpace X]
    [Finite I] {Z D : I ‚Üí Set X}
    (Z_closed : ‚àÄ i, IsClosed (Z i)) (D_clopen : ‚àÄ i, IsClopen (D i))
    (Z_subset_D : ‚àÄ i, Z i ‚äÜ D i) (Z_disj : univ.PairwiseDisjoint Z) :
    ‚àÉ C : I ‚Üí Set X, (‚àÄ i, IsClopen (C i)) ‚àß (‚àÄ i, Z i ‚äÜ C i) ‚àß (‚àÄ i, C i ‚äÜ D i) ‚àß
    (‚ãÉ i, D i) ‚äÜ (‚ãÉ i, C i) ‚àß (univ.PairwiseDisjoint C) := by
  induction I using Finite.induction_empty_option with
  | of_equiv e IH =>
    obtain ‚ü®C, h1, h2, h3, h4, h5‚ü© := IH (Z := Z ‚àò e) (D := D ‚àò e)
      (fun i ‚Ü¶ Z_closed (e i)) (fun i ‚Ü¶ D_clopen (e i))
      (fun i ‚Ü¶ Z_subset_D (e i)) (by simpa [‚Üê e.injective.injOn.pairwiseDisjoint_image])
    refine ‚ü®C ‚àò e.symm, fun i ‚Ü¶ h1 (e.symm i), fun i ‚Ü¶ by simpa using h2 (e.symm i),
      fun i ‚Ü¶ by simpa using h3 (e.symm i), ?_,
      by simpa [‚Üê e.symm.injective.injOn.pairwiseDisjoint_image]‚ü©
    simp only [Function.comp_apply, iUnion_subset_iff] at h4
    simpa [e.symm.surjective.iUnion_comp C] using fun i ‚Ü¶ h4 (e.symm i)
  | h_empty => exact ‚ü®fun _ ‚Ü¶ univ, by simp, by simp, by simp, by simp, fun i ‚Ü¶ PEmpty.elim i‚ü©
  | @h_option I _ IH =>
    -- let `Z'` be the restriction of `Z` along `some : I ‚Üí Option I`
    let Z' : I ‚Üí Set X := fun i ‚Ü¶ Z (some i)
    have Z'_closed (i : I) : IsClosed (Z (some i)) := Z_closed (some i)
    have Z'_disj : univ.PairwiseDisjoint (Z ‚àò some) := by
      rw [‚Üê (Option.some_injective _).injOn.pairwiseDisjoint_image]
      exact PairwiseDisjoint.subset Z_disj (by simp)
    -- find `Z none ‚äÜ V ‚äÜ D none \ ‚ãÉ Z'` using `exists_clopen_of_closed_subset_open`
    let U : Set X := D none \ ‚ãÉ i, Z (some i)
    have U_open : IsOpen U := IsOpen.sdiff (D_clopen none).2
      (isClosed_iUnion_of_finite (fun i ‚Ü¶ Z_closed (some i)))
    have Z0_subset_U : Z none ‚äÜ U := by
      rw [subset_diff]
      simpa using ‚ü®Z_subset_D none, fun i ‚Ü¶ (by apply Z_disj; all_goals simp)‚ü©
    obtain ‚ü®V, V_clopen, Z0_subset_V, V_subset_U‚ü© :=
      exists_clopen_of_closed_subset_open (Z_closed none) U_open Z0_subset_U
    have V_subset_D0 : V ‚äÜ D none := subset_trans V_subset_U diff_subset
    -- choose `Z' i ‚äÜ C' i ‚äÜ D' i = D i.succ \ V` using the inductive hypothesis
    let D' : I ‚Üí Set X := fun i ‚Ü¶ D (some i) \ V
    have D'_clopen (i : I) : IsClopen (D' i) := (D_clopen (some i)).diff V_clopen
    have Z'_subset_D' (i : I) : Z' i ‚äÜ D' i := by
      rw [subset_diff]
      refine ‚ü®by grind, Disjoint.mono_right V_subset_U ?_‚ü©
      exact Disjoint.mono_left (subset_iUnion_of_subset i fun _ h ‚Ü¶ h) (by grind)
    obtain ‚ü®C', C'_clopen, Z'_subset_C', C'_subset_D', C'_cover_D', C'_disj‚ü© :=
      IH Z'_closed D'_clopen Z'_subset_D' Z'_disj
    -- now choose `C0 = D none \ ‚ãÉ C' i`
    let C0 : Set X := D none \ ‚ãÉ i, C' i
    have : IsClopen C0 := (D_clopen none).diff (isClopen_iUnion_of_finite C'_clopen)
    have : Z none ‚äÜ C0 := by
      simp only [C0, subset_diff]
      exact ‚ü®by grind, Disjoint.mono_left Z0_subset_V (by simpa using by grind)‚ü©
    -- patch together to define `C none := C0`, `C (some i) := C' i`
    -- and verify the needed properties
    let C : Option I ‚Üí Set X := fun i ‚Ü¶ Option.casesOn i C0 C'
    refine ‚ü®C, ?_, ?_, ?_, ?_, ?_‚ü©
    all_goals try rintro (_ | i); all_goals grind
    ¬∑ intro x hx
      rw [mem_iUnion] at hx ‚ä¢
      by_cases hx0 : x ‚àà C0; { exact ‚ü®none, hx0‚ü© }
      by_cases hxD : x ‚àà D none
      ¬∑ have hxC' : x ‚àà ‚ãÉ i, C' i := by grind
        obtain ‚ü®i, hi‚ü© := mem_iUnion.mp hxC'
        exact ‚ü®some i, hi‚ü©
      ¬∑ obtain ‚ü®none | j, hi‚ü© := hx; {grind}
        have hxD' : x ‚àà ‚ãÉ i, D' i := mem_iUnion.mpr ‚ü®j, by grind‚ü©
        obtain ‚ü®k, hk‚ü© := mem_iUnion.mp <| C'_cover_D' hxD'
        exact ‚ü®some k, hk‚ü©
    ¬∑ rw [Set.pairwiseDisjoint_iff]
      rintro (_ | i) _ (_ | j) _
      ¬∑ simp
      ¬∑ simpa [C, C0, Set.not_nonempty_iff_eq_empty, ‚Üê Set.disjoint_iff_inter_eq_empty] using
          Disjoint.mono_right (subset_iUnion C' j) disjoint_sdiff_left
      ¬∑ simpa [C, C0, Set.not_nonempty_iff_eq_empty, ‚Üê Set.disjoint_iff_inter_eq_empty] using
          Disjoint.mono_left (subset_iUnion C' i) disjoint_sdiff_right
      ¬∑ simpa using (Set.pairwiseDisjoint_iff.mp C'_disj) (by trivial) (by trivial)

end LocallyCompact
