/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro
-/
import Mathlib.Tactic.StacksAttribute
import Mathlib.Topology.Compactness.Lindelof
import Mathlib.Topology.Separation.Hausdorff
import Mathlib.Topology.Connected.Clopen

/-!
# Regular, normal, T‚ÇÉ, T‚ÇÑ and T‚ÇÖ spaces

This file continues the study of separation properties of topological spaces, focussing
on conditions strictly stronger than T‚ÇÇ.

## Main definitions

* `RegularSpace`: A regular space is one where, given any closed `C` and `x ‚àâ C`,
  there are disjoint open sets containing `x` and `C` respectively. Such a space is not necessarily
  Hausdorff.
* `T3Space`: A T‚ÇÉ space is a regular T‚ÇÄ space. T‚ÇÉ implies T‚ÇÇ.‚ÇÖ.
* `NormalSpace`: A normal space, is one where given two disjoint closed sets,
  we can find two open sets that separate them. Such a space is not necessarily Hausdorff, even if
  it is T‚ÇÄ.
* `T4Space`: A T‚ÇÑ space is a normal T‚ÇÅ space. T‚ÇÑ implies T‚ÇÉ.
* `CompletelyNormalSpace`: A completely normal space is one in which for any two sets `s`, `t`
  such that if both `closure s` is disjoint with `t`, and `s` is disjoint with `closure t`,
  then there exist disjoint neighbourhoods of `s` and `t`. `Embedding.completelyNormalSpace` allows
  us to conclude that this is equivalent to all subspaces being normal. Such a space is not
  necessarily Hausdorff or regular, even if it is T‚ÇÄ.
* `T5Space`: A T‚ÇÖ space is a completely normal T‚ÇÅ space. T‚ÇÖ implies T‚ÇÑ.

See `Mathlib/Topology/Separation/GDelta.lean` for the definitions of `PerfectlyNormalSpace` and
`T6Space`.

Note that `mathlib` adopts the modern convention that `m ‚â§ n` if and only if `T_m ‚Üí T_n`, but
occasionally the literature swaps definitions for e.g. T‚ÇÉ and regular.

## Main results

### Regular spaces

If the space is also Lindel√∂f:

* `NormalSpace.of_regularSpace_lindelofSpace`: every regular Lindel√∂f space is normal.

### T‚ÇÉ spaces

* `disjoint_nested_nhds`: Given two points `x ‚â† y`, we can find neighbourhoods `x ‚àà V‚ÇÅ ‚äÜ U‚ÇÅ` and
  `y ‚àà V‚ÇÇ ‚äÜ U‚ÇÇ`, with the `V‚Çñ` closed and the `U‚Çñ` open, such that the `U‚Çñ` are disjoint.

## References

* <https://en.wikipedia.org/wiki/Separation_axiom>
* <https://en.wikipedia.org/wiki/Normal_space>
* [Willard's *General Topology*][zbMATH02107988]

-/

assert_not_exists UniformSpace

open Function Set Filter Topology TopologicalSpace

universe u v

variable {X : Type*} {Y : Type*} [TopologicalSpace X]

section RegularSpace

/-- A topological space is called a *regular space* if for any closed set `s` and `a ‚àâ s`, there
exist disjoint open sets `U ‚äá s` and `V ‚àã a`. We formulate this condition in terms of `Disjoint`ness
of filters `ùìùÀ¢ s` and `ùìù a`. -/
@[mk_iff]
class RegularSpace (X : Type u) [TopologicalSpace X] : Prop where
  /-- If `a` is a point that does not belong to a closed set `s`, then `a` and `s` admit disjoint
  neighborhoods. -/
  regular : ‚àÄ {s : Set X} {a}, IsClosed s ‚Üí a ‚àâ s ‚Üí Disjoint (ùìùÀ¢ s) (ùìù a)

theorem regularSpace_TFAE (X : Type u) [TopologicalSpace X] :
    List.TFAE [RegularSpace X,
      ‚àÄ (s : Set X) x, x ‚àâ closure s ‚Üí Disjoint (ùìùÀ¢ s) (ùìù x),
      ‚àÄ (x : X) (s : Set X), Disjoint (ùìùÀ¢ s) (ùìù x) ‚Üî x ‚àâ closure s,
      ‚àÄ (x : X) (s : Set X), s ‚àà ùìù x ‚Üí ‚àÉ t ‚àà ùìù x, IsClosed t ‚àß t ‚äÜ s,
      ‚àÄ x : X, (ùìù x).lift' closure ‚â§ ùìù x,
      ‚àÄ x : X , (ùìù x).lift' closure = ùìù x] := by
  tfae_have 1 ‚Üî 5 := by
    rw [regularSpace_iff, (@compl_surjective (Set X) _).forall, forall_swap]
    simp only [isClosed_compl_iff, mem_compl_iff, Classical.not_not, @and_comm (_ ‚àà _),
      (nhds_basis_opens _).lift'_closure.le_basis_iff (nhds_basis_opens _), and_imp,
      (nhds_basis_opens _).disjoint_iff_right, exists_prop, ‚Üê subset_interior_iff_mem_nhdsSet,
      interior_compl, compl_subset_compl]
  tfae_have 5 ‚Üí 6 := fun h a => (h a).antisymm (ùìù _).le_lift'_closure
  tfae_have 6 ‚Üí 4
  | H, a, s, hs => by
    rw [‚Üê H] at hs
    rcases (ùìù a).basis_sets.lift'_closure.mem_iff.mp hs with ‚ü®U, hU, hUs‚ü©
    exact ‚ü®closure U, mem_of_superset hU subset_closure, isClosed_closure, hUs‚ü©
  tfae_have 4 ‚Üí 2
  | H, s, a, ha => by
    have ha' : s·∂ú ‚àà ùìù a := by rwa [‚Üê mem_interior_iff_mem_nhds, interior_compl]
    rcases H _ _ ha' with ‚ü®U, hU, hUc, hUs‚ü©
    refine disjoint_of_disjoint_of_mem disjoint_compl_left ?_ hU
    rwa [‚Üê subset_interior_iff_mem_nhdsSet, hUc.isOpen_compl.interior_eq, subset_compl_comm]
  tfae_have 2 ‚Üí 3 := by
    refine fun H a s => ‚ü®fun hd has => mem_closure_iff_nhds_ne_bot.mp has ?_, H s a‚ü©
    exact (hd.symm.mono_right <| @principal_le_nhdsSet _ _ s).eq_bot
  tfae_have 3 ‚Üí 1 := fun H => ‚ü®fun hs ha => (H _ _).mpr <| hs.closure_eq.symm ‚ñ∏ ha‚ü©
  tfae_finish

theorem RegularSpace.of_lift'_closure_le (h : ‚àÄ x : X, (ùìù x).lift' closure ‚â§ ùìù x) :
    RegularSpace X :=
  Iff.mpr ((regularSpace_TFAE X).out 0 4) h

theorem RegularSpace.of_lift'_closure (h : ‚àÄ x : X, (ùìù x).lift' closure = ùìù x) : RegularSpace X :=
  Iff.mpr ((regularSpace_TFAE X).out 0 5) h

theorem RegularSpace.of_hasBasis {Œπ : X ‚Üí Sort*} {p : ‚àÄ a, Œπ a ‚Üí Prop} {s : ‚àÄ a, Œπ a ‚Üí Set X}
    (h‚ÇÅ : ‚àÄ a, (ùìù a).HasBasis (p a) (s a)) (h‚ÇÇ : ‚àÄ a i, p a i ‚Üí IsClosed (s a i)) :
    RegularSpace X :=
  .of_lift'_closure fun a => (h‚ÇÅ a).lift'_closure_eq_self (h‚ÇÇ a)

theorem RegularSpace.of_exists_mem_nhds_isClosed_subset
    (h : ‚àÄ (x : X), ‚àÄ s ‚àà ùìù x, ‚àÉ t ‚àà ùìù x, IsClosed t ‚àß t ‚äÜ s) : RegularSpace X :=
  Iff.mpr ((regularSpace_TFAE X).out 0 3) h

/-- A weakly locally compact R‚ÇÅ space is regular. -/
instance (priority := 100) [WeaklyLocallyCompactSpace X] [R1Space X] : RegularSpace X :=
  .of_hasBasis isCompact_isClosed_basis_nhds fun _ _ ‚ü®_, _, h‚ü© ‚Ü¶ h

section
variable [RegularSpace X] {x : X} {s : Set X}

theorem disjoint_nhdsSet_nhds : Disjoint (ùìùÀ¢ s) (ùìù x) ‚Üî x ‚àâ closure s := by
  have h := (regularSpace_TFAE X).out 0 2
  exact h.mp ‚Äπ_‚Ä∫ _ _

theorem disjoint_nhds_nhdsSet : Disjoint (ùìù x) (ùìùÀ¢ s) ‚Üî x ‚àâ closure s :=
  disjoint_comm.trans disjoint_nhdsSet_nhds

/-- A regular space is R‚ÇÅ. -/
instance (priority := 100) : R1Space X where
  specializes_or_disjoint_nhds _ _ := or_iff_not_imp_left.2 fun h ‚Ü¶ by
    rwa [‚Üê nhdsSet_singleton, disjoint_nhdsSet_nhds, ‚Üê specializes_iff_mem_closure]

theorem exists_mem_nhds_isClosed_subset {x : X} {s : Set X} (h : s ‚àà ùìù x) :
    ‚àÉ t ‚àà ùìù x, IsClosed t ‚àß t ‚äÜ s := by
  have h' := (regularSpace_TFAE X).out 0 3
  exact h'.mp ‚Äπ_‚Ä∫ _ _ h

theorem closed_nhds_basis (x : X) : (ùìù x).HasBasis (fun s : Set X => s ‚àà ùìù x ‚àß IsClosed s) id :=
  hasBasis_self.2 fun _ => exists_mem_nhds_isClosed_subset

theorem lift'_nhds_closure (x : X) : (ùìù x).lift' closure = ùìù x :=
  (closed_nhds_basis x).lift'_closure_eq_self fun _ => And.right

theorem Filter.HasBasis.nhds_closure {Œπ : Sort*} {x : X} {p : Œπ ‚Üí Prop} {s : Œπ ‚Üí Set X}
    (h : (ùìù x).HasBasis p s) : (ùìù x).HasBasis p fun i => closure (s i) :=
  lift'_nhds_closure x ‚ñ∏ h.lift'_closure

theorem hasBasis_nhds_closure (x : X) : (ùìù x).HasBasis (fun s => s ‚àà ùìù x) closure :=
  (ùìù x).basis_sets.nhds_closure

theorem hasBasis_opens_closure (x : X) : (ùìù x).HasBasis (fun s => x ‚àà s ‚àß IsOpen s) closure :=
  (nhds_basis_opens x).nhds_closure

theorem IsCompact.exists_isOpen_closure_subset {K U : Set X} (hK : IsCompact K) (hU : U ‚àà ùìùÀ¢ K) :
    ‚àÉ V, IsOpen V ‚àß K ‚äÜ V ‚àß closure V ‚äÜ U := by
  have hd : Disjoint (ùìùÀ¢ K) (ùìùÀ¢ U·∂ú) := by
    simpa [hK.disjoint_nhdsSet_left, disjoint_nhds_nhdsSet,
      ‚Üê subset_interior_iff_mem_nhdsSet] using hU
  rcases ((hasBasis_nhdsSet _).disjoint_iff (hasBasis_nhdsSet _)).1 hd
    with ‚ü®V, ‚ü®hVo, hKV‚ü©, W, ‚ü®hW, hUW‚ü©, hVW‚ü©
  refine ‚ü®V, hVo, hKV, Subset.trans ?_ (compl_subset_comm.1 hUW)‚ü©
  exact closure_minimal hVW.subset_compl_right hW.isClosed_compl

theorem IsCompact.lift'_closure_nhdsSet {K : Set X} (hK : IsCompact K) :
    (ùìùÀ¢ K).lift' closure = ùìùÀ¢ K := by
  refine le_antisymm (fun U hU ‚Ü¶ ?_) (le_lift'_closure _)
  rcases hK.exists_isOpen_closure_subset hU with ‚ü®V, hVo, hKV, hVU‚ü©
  exact mem_of_superset (mem_lift' <| hVo.mem_nhdsSet.2 hKV) hVU

theorem TopologicalSpace.IsTopologicalBasis.nhds_basis_closure {B : Set (Set X)}
    (hB : IsTopologicalBasis B) (x : X) :
    (ùìù x).HasBasis (fun s : Set X => x ‚àà s ‚àß s ‚àà B) closure := by
  simpa only [and_comm] using hB.nhds_hasBasis.nhds_closure

theorem TopologicalSpace.IsTopologicalBasis.exists_closure_subset {B : Set (Set X)}
    (hB : IsTopologicalBasis B) {x : X} {s : Set X} (h : s ‚àà ùìù x) :
    ‚àÉ t ‚àà B, x ‚àà t ‚àß closure t ‚äÜ s := by
  simpa only [exists_prop, and_assoc] using hB.nhds_hasBasis.nhds_closure.mem_iff.mp h

protected theorem Topology.IsInducing.regularSpace [TopologicalSpace Y] {f : Y ‚Üí X}
    (hf : IsInducing f) : RegularSpace Y :=
  .of_hasBasis
    (fun b => by rw [hf.nhds_eq_comap b]; exact (closed_nhds_basis _).comap _)
    fun b s hs => by exact hs.2.preimage hf.continuous

@[deprecated (since := "2024-10-28")] alias Inducing.regularSpace := IsInducing.regularSpace

theorem regularSpace_induced (f : Y ‚Üí X) : @RegularSpace Y (induced f ‚Äπ_‚Ä∫) :=
  letI := induced f ‚Äπ_‚Ä∫
  (IsInducing.induced f).regularSpace

theorem regularSpace_sInf {X} {T : Set (TopologicalSpace X)} (h : ‚àÄ t ‚àà T, @RegularSpace X t) :
    @RegularSpace X (sInf T) := by
  let _ := sInf T
  have : ‚àÄ a, (ùìù a).HasBasis
      (fun If : Œ£ I : Set T, I ‚Üí Set X =>
        If.1.Finite ‚àß ‚àÄ i : If.1, If.2 i ‚àà @nhds X i a ‚àß @IsClosed X i (If.2 i))
      fun If => ‚ãÇ i : If.1, If.snd i := fun a ‚Ü¶ by
    rw [nhds_sInf, ‚Üê iInf_subtype'']
    exact .iInf fun t : T => @closed_nhds_basis X t (h t t.2) a
  refine .of_hasBasis this fun a If hIf => isClosed_iInter fun i => ?_
  exact (hIf.2 i).2.mono (sInf_le (i : T).2)

theorem regularSpace_iInf {Œπ X} {t : Œπ ‚Üí TopologicalSpace X} (h : ‚àÄ i, @RegularSpace X (t i)) :
    @RegularSpace X (iInf t) :=
  regularSpace_sInf <| forall_mem_range.mpr h

theorem RegularSpace.inf {X} {t‚ÇÅ t‚ÇÇ : TopologicalSpace X} (h‚ÇÅ : @RegularSpace X t‚ÇÅ)
    (h‚ÇÇ : @RegularSpace X t‚ÇÇ) : @RegularSpace X (t‚ÇÅ ‚äì t‚ÇÇ) := by
  rw [inf_eq_iInf]
  exact regularSpace_iInf (Bool.forall_bool.2 ‚ü®h‚ÇÇ, h‚ÇÅ‚ü©)

instance {p : X ‚Üí Prop} : RegularSpace (Subtype p) :=
  IsEmbedding.subtypeVal.isInducing.regularSpace

instance [TopologicalSpace Y] [RegularSpace Y] : RegularSpace (X √ó Y) :=
  (regularSpace_induced (@Prod.fst X Y)).inf (regularSpace_induced (@Prod.snd X Y))

instance {Œπ : Type*} {X : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (X i)] [‚àÄ i, RegularSpace (X i)] :
    RegularSpace (‚àÄ i, X i) :=
  regularSpace_iInf fun _ => regularSpace_induced _

/-- In a regular space, if a compact set and a closed set are disjoint, then they have disjoint
neighborhoods. -/
lemma SeparatedNhds.of_isCompact_isClosed {s t : Set X}
    (hs : IsCompact s) (ht : IsClosed t) (hst : Disjoint s t) : SeparatedNhds s t := by
  simpa only [separatedNhds_iff_disjoint, hs.disjoint_nhdsSet_left, disjoint_nhds_nhdsSet,
    ht.closure_eq, disjoint_left] using hst

end

/-- This technique to witness `HasSeparatingCover` in regular Lindel√∂f topological spaces
will be used to prove regular Lindel√∂f spaces are normal. -/
lemma IsClosed.HasSeparatingCover {s t : Set X} [LindelofSpace X] [RegularSpace X]
    (s_cl : IsClosed s) (t_cl : IsClosed t) (st_dis : Disjoint s t) : HasSeparatingCover s t := by
  -- `IsLindelof.indexed_countable_subcover` requires the space be Nonempty
  rcases isEmpty_or_nonempty X with empty_X | nonempty_X
  ¬∑ rw [subset_eq_empty (t := s) (fun ‚¶É_‚¶Ñ _ ‚Ü¶ trivial) (univ_eq_empty_iff.mpr empty_X)]
    exact hasSeparatingCovers_iff_separatedNhds.mpr (SeparatedNhds.empty_left t) |>.1
  -- This is almost `HasSeparatingCover`, but is not countable. We define for all `a : X` for use
  -- with `IsLindelof.indexed_countable_subcover` momentarily.
  have (a : X) : ‚àÉ n : Set X, IsOpen n ‚àß Disjoint (closure n) t ‚àß (a ‚àà s ‚Üí a ‚àà n) := by
    wlog ains : a ‚àà s
    ¬∑ exact ‚ü®‚àÖ, isOpen_empty, SeparatedNhds.empty_left t |>.disjoint_closure_left, fun a ‚Ü¶ ains a‚ü©
    obtain ‚ü®n, nna, ncl, nsubkc‚ü© := ((regularSpace_TFAE X).out 0 3 :).mp ‚ÄπRegularSpace X‚Ä∫ a t·∂ú <|
      t_cl.compl_mem_nhds (disjoint_left.mp st_dis ains)
    exact
      ‚ü®interior n,
       isOpen_interior,
       disjoint_left.mpr fun ‚¶É_‚¶Ñ ain ‚Ü¶
         nsubkc <| (IsClosed.closure_subset_iff ncl).mpr interior_subset ain,
       fun _ ‚Ü¶ mem_interior_iff_mem_nhds.mpr nna‚ü©
  -- By Lindel√∂f, we may obtain a countable subcover witnessing `HasSeparatingCover`
  choose u u_open u_dis u_nhds using this
  obtain ‚ü®f, f_cov‚ü© := s_cl.isLindelof.indexed_countable_subcover
    u u_open (fun a ainh ‚Ü¶ mem_iUnion.mpr ‚ü®a, u_nhds a ainh‚ü©)
  exact ‚ü®u ‚àò f, f_cov, fun n ‚Ü¶ ‚ü®u_open (f n), u_dis (f n)‚ü©‚ü©


end RegularSpace

section LocallyCompactRegularSpace

/-- In a (possibly non-Hausdorff) locally compact regular space, for every containment `K ‚äÜ U` of
  a compact set `K` in an open set `U`, there is a compact closed neighborhood `L`
  such that `K ‚äÜ L ‚äÜ U`: equivalently, there is a compact closed set `L` such
  that `K ‚äÜ interior L` and `L ‚äÜ U`. -/
theorem exists_compact_closed_between [LocallyCompactSpace X] [RegularSpace X]
    {K U : Set X} (hK : IsCompact K) (hU : IsOpen U) (h_KU : K ‚äÜ U) :
    ‚àÉ L, IsCompact L ‚àß IsClosed L ‚àß K ‚äÜ interior L ‚àß L ‚äÜ U :=
  let ‚ü®L, L_comp, KL, LU‚ü© := exists_compact_between hK hU h_KU
  ‚ü®closure L, L_comp.closure, isClosed_closure, KL.trans <| interior_mono subset_closure,
    L_comp.closure_subset_of_isOpen hU LU‚ü©

/-- In a locally compact regular space, given a compact set `K` inside an open set `U`, we can find
an open set `V` between these sets with compact closure: `K ‚äÜ V` and the closure of `V` is
inside `U`. -/
theorem exists_open_between_and_isCompact_closure [LocallyCompactSpace X] [RegularSpace X]
    {K U : Set X} (hK : IsCompact K) (hU : IsOpen U) (hKU : K ‚äÜ U) :
    ‚àÉ V, IsOpen V ‚àß K ‚äÜ V ‚àß closure V ‚äÜ U ‚àß IsCompact (closure V) := by
  rcases exists_compact_closed_between hK hU hKU with ‚ü®L, L_compact, L_closed, KL, LU‚ü©
  have A : closure (interior L) ‚äÜ L := by
    apply (closure_mono interior_subset).trans (le_of_eq L_closed.closure_eq)
  refine ‚ü®interior L, isOpen_interior, KL, A.trans LU, ?_‚ü©
  exact L_compact.closure_of_subset interior_subset

end LocallyCompactRegularSpace

section T25

/-- A T‚ÇÇ.‚ÇÖ space, also known as a Urysohn space, is a topological space
  where for every pair `x ‚â† y`, there are two open sets, with the intersection of closures
  empty, one containing `x` and the other `y` . -/
class T25Space (X : Type u) [TopologicalSpace X] : Prop where
  /-- Given two distinct points in a T‚ÇÇ.‚ÇÖ space, their filters of closed neighborhoods are
  disjoint. -/
  t2_5 : ‚àÄ ‚¶Éx y : X‚¶Ñ, x ‚â† y ‚Üí Disjoint ((ùìù x).lift' closure) ((ùìù y).lift' closure)

@[simp]
theorem disjoint_lift'_closure_nhds [T25Space X] {x y : X} :
    Disjoint ((ùìù x).lift' closure) ((ùìù y).lift' closure) ‚Üî x ‚â† y :=
  ‚ü®fun h hxy => by simp [hxy, nhds_neBot.ne] at h, fun h => T25Space.t2_5 h‚ü©

-- see Note [lower instance priority]
instance (priority := 100) T25Space.t2Space [T25Space X] : T2Space X :=
  t2Space_iff_disjoint_nhds.2 fun _ _ hne =>
    (disjoint_lift'_closure_nhds.2 hne).mono (le_lift'_closure _) (le_lift'_closure _)

theorem exists_nhds_disjoint_closure [T25Space X] {x y : X} (h : x ‚â† y) :
    ‚àÉ s ‚àà ùìù x, ‚àÉ t ‚àà ùìù y, Disjoint (closure s) (closure t) :=
  ((ùìù x).basis_sets.lift'_closure.disjoint_iff (ùìù y).basis_sets.lift'_closure).1 <|
    disjoint_lift'_closure_nhds.2 h

theorem exists_open_nhds_disjoint_closure [T25Space X] {x y : X} (h : x ‚â† y) :
    ‚àÉ u : Set X,
      x ‚àà u ‚àß IsOpen u ‚àß ‚àÉ v : Set X, y ‚àà v ‚àß IsOpen v ‚àß Disjoint (closure u) (closure v) := by
  simpa only [exists_prop, and_assoc] using
    ((nhds_basis_opens x).lift'_closure.disjoint_iff (nhds_basis_opens y).lift'_closure).1
      (disjoint_lift'_closure_nhds.2 h)

theorem T25Space.of_injective_continuous [TopologicalSpace Y] [T25Space Y] {f : X ‚Üí Y}
    (hinj : Injective f) (hcont : Continuous f) : T25Space X where
  t2_5 x y hne := (tendsto_lift'_closure_nhds hcont x).disjoint (t2_5 <| hinj.ne hne)
    (tendsto_lift'_closure_nhds hcont y)

theorem Topology.IsEmbedding.t25Space [TopologicalSpace Y] [T25Space Y] {f : X ‚Üí Y}
    (hf : IsEmbedding f) : T25Space X :=
  .of_injective_continuous hf.injective hf.continuous

@[deprecated (since := "2024-10-26")]
alias Embedding.t25Space := IsEmbedding.t25Space

protected theorem Homeomorph.t25Space [TopologicalSpace Y] [T25Space X] (h : X ‚âÉ‚Çú Y) : T25Space Y :=
  h.symm.isEmbedding.t25Space

instance Subtype.instT25Space [T25Space X] {p : X ‚Üí Prop} : T25Space {x // p x} :=
  IsEmbedding.subtypeVal.t25Space

end T25

section T3

/-- A T‚ÇÉ space is a T‚ÇÄ space which is a regular space. Any T‚ÇÉ space is a T‚ÇÅ space, a T‚ÇÇ space, and
a T‚ÇÇ.‚ÇÖ space. -/
class T3Space (X : Type u) [TopologicalSpace X] : Prop extends T0Space X, RegularSpace X

instance (priority := 90) instT3Space [T0Space X] [RegularSpace X] : T3Space X := ‚ü®‚ü©

theorem RegularSpace.t3Space_iff_t0Space [RegularSpace X] : T3Space X ‚Üî T0Space X := by
  constructor <;> intro <;> infer_instance

-- see Note [lower instance priority]
instance (priority := 100) T3Space.t25Space [T3Space X] : T25Space X := by
  refine ‚ü®fun x y hne => ?_‚ü©
  rw [lift'_nhds_closure, lift'_nhds_closure]
  have : x ‚àâ closure {y} ‚à® y ‚àâ closure {x} :=
    (t0Space_iff_or_notMem_closure X).mp inferInstance hne
  simp only [‚Üê disjoint_nhds_nhdsSet, nhdsSet_singleton] at this
  exact this.elim id fun h => h.symm

protected theorem Topology.IsEmbedding.t3Space [TopologicalSpace Y] [T3Space Y] {f : X ‚Üí Y}
    (hf : IsEmbedding f) : T3Space X :=
  { toT0Space := hf.t0Space
    toRegularSpace := hf.isInducing.regularSpace }

@[deprecated (since := "2024-10-26")]
alias Embedding.t3Space := IsEmbedding.t3Space

protected theorem Homeomorph.t3Space [TopologicalSpace Y] [T3Space X] (h : X ‚âÉ‚Çú Y) : T3Space Y :=
  h.symm.isEmbedding.t3Space

instance Subtype.t3Space [T3Space X] {p : X ‚Üí Prop} : T3Space (Subtype p) :=
  IsEmbedding.subtypeVal.t3Space

instance ULift.instT3Space [T3Space X] : T3Space (ULift X) :=
  IsEmbedding.uliftDown.t3Space

instance [TopologicalSpace Y] [T3Space X] [T3Space Y] : T3Space (X √ó Y) := ‚ü®‚ü©

instance {Œπ : Type*} {X : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (X i)] [‚àÄ i, T3Space (X i)] :
    T3Space (‚àÄ i, X i) := ‚ü®‚ü©

/-- Given two points `x ‚â† y`, we can find neighbourhoods `x ‚àà V‚ÇÅ ‚äÜ U‚ÇÅ` and `y ‚àà V‚ÇÇ ‚äÜ U‚ÇÇ`,
with the `V‚Çñ` closed and the `U‚Çñ` open, such that the `U‚Çñ` are disjoint. -/
theorem disjoint_nested_nhds [T3Space X] {x y : X} (h : x ‚â† y) :
    ‚àÉ U‚ÇÅ ‚àà ùìù x, ‚àÉ V‚ÇÅ ‚àà ùìù x, ‚àÉ U‚ÇÇ ‚àà ùìù y, ‚àÉ V‚ÇÇ ‚àà ùìù y,
      IsClosed V‚ÇÅ ‚àß IsClosed V‚ÇÇ ‚àß IsOpen U‚ÇÅ ‚àß IsOpen U‚ÇÇ ‚àß V‚ÇÅ ‚äÜ U‚ÇÅ ‚àß V‚ÇÇ ‚äÜ U‚ÇÇ ‚àß Disjoint U‚ÇÅ U‚ÇÇ := by
  rcases t2_separation h with ‚ü®U‚ÇÅ, U‚ÇÇ, U‚ÇÅ_op, U‚ÇÇ_op, x_in, y_in, H‚ü©
  rcases exists_mem_nhds_isClosed_subset (U‚ÇÅ_op.mem_nhds x_in) with ‚ü®V‚ÇÅ, V‚ÇÅ_in, V‚ÇÅ_closed, h‚ÇÅ‚ü©
  rcases exists_mem_nhds_isClosed_subset (U‚ÇÇ_op.mem_nhds y_in) with ‚ü®V‚ÇÇ, V‚ÇÇ_in, V‚ÇÇ_closed, h‚ÇÇ‚ü©
  exact ‚ü®U‚ÇÅ, mem_of_superset V‚ÇÅ_in h‚ÇÅ, V‚ÇÅ, V‚ÇÅ_in, U‚ÇÇ, mem_of_superset V‚ÇÇ_in h‚ÇÇ, V‚ÇÇ, V‚ÇÇ_in,
    V‚ÇÅ_closed, V‚ÇÇ_closed, U‚ÇÅ_op, U‚ÇÇ_op, h‚ÇÅ, h‚ÇÇ, H‚ü©

open SeparationQuotient

/-- The `SeparationQuotient` of a regular space is a T‚ÇÉ space. -/
instance [RegularSpace X] : T3Space (SeparationQuotient X) where
  regular {s a} hs ha := by
    rcases surjective_mk a with ‚ü®a, rfl‚ü©
    rw [‚Üê disjoint_comap_iff surjective_mk, comap_mk_nhds_mk, comap_mk_nhdsSet]
    exact RegularSpace.regular (hs.preimage continuous_mk) ha

end T3

section NormalSpace

/-- A topological space is said to be a *normal space* if any two disjoint closed sets
have disjoint open neighborhoods. -/
class NormalSpace (X : Type u) [TopologicalSpace X] : Prop where
  /-- Two disjoint sets in a normal space admit disjoint neighbourhoods. -/
  normal : ‚àÄ s t : Set X, IsClosed s ‚Üí IsClosed t ‚Üí Disjoint s t ‚Üí SeparatedNhds s t

theorem normal_separation [NormalSpace X] {s t : Set X} (H1 : IsClosed s) (H2 : IsClosed t)
    (H3 : Disjoint s t) : SeparatedNhds s t :=
  NormalSpace.normal s t H1 H2 H3

theorem disjoint_nhdsSet_nhdsSet [NormalSpace X] {s t : Set X} (hs : IsClosed s) (ht : IsClosed t)
    (hd : Disjoint s t) : Disjoint (ùìùÀ¢ s) (ùìùÀ¢ t) :=
  (normal_separation hs ht hd).disjoint_nhdsSet

theorem normal_exists_closure_subset [NormalSpace X] {s t : Set X} (hs : IsClosed s) (ht : IsOpen t)
    (hst : s ‚äÜ t) : ‚àÉ u, IsOpen u ‚àß s ‚äÜ u ‚àß closure u ‚äÜ t := by
  have : Disjoint s t·∂ú := Set.disjoint_left.mpr fun x hxs hxt => hxt (hst hxs)
  rcases normal_separation hs (isClosed_compl_iff.2 ht) this with
    ‚ü®s', t', hs', ht', hss', htt', hs't'‚ü©
  refine ‚ü®s', hs', hss', Subset.trans (closure_minimal ?_ (isClosed_compl_iff.2 ht'))
    (compl_subset_comm.1 htt')‚ü©
  exact fun x hxs hxt => hs't'.le_bot ‚ü®hxs, hxt‚ü©

/-- If the codomain of a closed embedding is a normal space, then so is the domain. -/
protected theorem Topology.IsClosedEmbedding.normalSpace [TopologicalSpace Y] [NormalSpace Y]
    {f : X ‚Üí Y} (hf : IsClosedEmbedding f) : NormalSpace X where
  normal s t hs ht hst := by
    have H : SeparatedNhds (f '' s) (f '' t) :=
      NormalSpace.normal (f '' s) (f '' t) (hf.isClosedMap s hs) (hf.isClosedMap t ht)
        (disjoint_image_of_injective hf.injective hst)
    exact (H.preimage hf.continuous).mono (subset_preimage_image _ _) (subset_preimage_image _ _)

protected theorem Homeomorph.normalSpace [TopologicalSpace Y] [NormalSpace X] (h : X ‚âÉ‚Çú Y) :
    NormalSpace Y :=
  h.symm.isClosedEmbedding.normalSpace

instance (priority := 100) NormalSpace.of_compactSpace_r1Space [CompactSpace X] [R1Space X] :
    NormalSpace X where
  normal _s _t hs ht := .of_isCompact_isCompact_isClosed hs.isCompact ht.isCompact ht

/-- A regular topological space with a Lindel√∂f topology is a normal space. A consequence of e.g.
Corollaries 20.8 and 20.10 of [Willard's *General Topology*][zbMATH02107988] (without the
assumption of Hausdorff). -/
instance (priority := 100) NormalSpace.of_regularSpace_lindelofSpace
    [RegularSpace X] [LindelofSpace X] : NormalSpace X where
  normal _ _ hcl kcl hkdis :=
    hasSeparatingCovers_iff_separatedNhds.mp
    ‚ü®hcl.HasSeparatingCover kcl hkdis, kcl.HasSeparatingCover hcl (Disjoint.symm hkdis)‚ü©

instance (priority := 100) NormalSpace.of_regularSpace_secondCountableTopology
    [RegularSpace X] [SecondCountableTopology X] : NormalSpace X :=
  of_regularSpace_lindelofSpace

end NormalSpace

section Normality

/-- A T‚ÇÑ space is a normal T‚ÇÅ space. -/
class T4Space (X : Type u) [TopologicalSpace X] : Prop extends T1Space X, NormalSpace X

instance (priority := 100) [T1Space X] [NormalSpace X] : T4Space X := ‚ü®‚ü©

-- see Note [lower instance priority]
instance (priority := 100) T4Space.t3Space [T4Space X] : T3Space X where
  regular hs hxs := by simpa only [nhdsSet_singleton] using (normal_separation hs isClosed_singleton
    (disjoint_singleton_right.mpr hxs)).disjoint_nhdsSet

/-- If the codomain of a closed embedding is a T‚ÇÑ space, then so is the domain. -/
protected theorem Topology.IsClosedEmbedding.t4Space [TopologicalSpace Y] [T4Space Y] {f : X ‚Üí Y}
    (hf : IsClosedEmbedding f) : T4Space X where
  toT1Space := hf.isEmbedding.t1Space
  toNormalSpace := hf.normalSpace

protected theorem Homeomorph.t4Space [TopologicalSpace Y] [T4Space X] (h : X ‚âÉ‚Çú Y) : T4Space Y :=
  h.symm.isClosedEmbedding.t4Space

instance ULift.instT4Space [T4Space X] : T4Space (ULift X) := IsClosedEmbedding.uliftDown.t4Space

namespace SeparationQuotient

/-- The `SeparationQuotient` of a normal space is a normal space. -/
instance [NormalSpace X] : NormalSpace (SeparationQuotient X) where
  normal s t hs ht hd := separatedNhds_iff_disjoint.2 <| by
    rw [‚Üê disjoint_comap_iff surjective_mk, comap_mk_nhdsSet, comap_mk_nhdsSet]
    exact disjoint_nhdsSet_nhdsSet (hs.preimage continuous_mk) (ht.preimage continuous_mk)
      (hd.preimage mk)

end SeparationQuotient

variable (X)

end Normality

section CompletelyNormal

/-- A topological space `X` is a *completely normal space* provided that for any two sets `s`, `t`
such that if both `closure s` is disjoint with `t`, and `s` is disjoint with `closure t`,
then there exist disjoint neighbourhoods of `s` and `t`. -/
class CompletelyNormalSpace (X : Type u) [TopologicalSpace X] : Prop where
  /-- If `closure s` is disjoint with `t`, and `s` is disjoint with `closure t`, then `s` and `t`
  admit disjoint neighbourhoods. -/
  completely_normal :
    ‚àÄ ‚¶És t : Set X‚¶Ñ, Disjoint (closure s) t ‚Üí Disjoint s (closure t) ‚Üí Disjoint (ùìùÀ¢ s) (ùìùÀ¢ t)

export CompletelyNormalSpace (completely_normal)

-- see Note [lower instance priority]
/-- A completely normal space is a normal space. -/
instance (priority := 100) CompletelyNormalSpace.toNormalSpace
    [CompletelyNormalSpace X] : NormalSpace X where
  normal s t hs ht hd := separatedNhds_iff_disjoint.2 <|
    completely_normal (by rwa [hs.closure_eq]) (by rwa [ht.closure_eq])

theorem Topology.IsEmbedding.completelyNormalSpace [TopologicalSpace Y] [CompletelyNormalSpace Y]
    {e : X ‚Üí Y} (he : IsEmbedding e) : CompletelyNormalSpace X := by
  refine ‚ü®fun s t hd‚ÇÅ hd‚ÇÇ => ?_‚ü©
  simp only [he.isInducing.nhdsSet_eq_comap]
  refine disjoint_comap (completely_normal ?_ ?_)
  ¬∑ rwa [‚Üê subset_compl_iff_disjoint_left, image_subset_iff, preimage_compl,
      ‚Üê he.closure_eq_preimage_closure_image, subset_compl_iff_disjoint_left]
  ¬∑ rwa [‚Üê subset_compl_iff_disjoint_right, image_subset_iff, preimage_compl,
      ‚Üê he.closure_eq_preimage_closure_image, subset_compl_iff_disjoint_right]

@[deprecated (since := "2024-10-26")]
alias Embedding.completelyNormalSpace := IsEmbedding.completelyNormalSpace

/-- A subspace of a completely normal space is a completely normal space. -/
instance [CompletelyNormalSpace X] {p : X ‚Üí Prop} : CompletelyNormalSpace { x // p x } :=
  IsEmbedding.subtypeVal.completelyNormalSpace

instance ULift.instCompletelyNormalSpace [CompletelyNormalSpace X] :
    CompletelyNormalSpace (ULift X) :=
  IsEmbedding.uliftDown.completelyNormalSpace

/-- A T‚ÇÖ space is a completely normal T‚ÇÅ space. -/
class T5Space (X : Type u) [TopologicalSpace X] : Prop extends T1Space X, CompletelyNormalSpace X

theorem Topology.IsEmbedding.t5Space [TopologicalSpace Y] [T5Space Y] {e : X ‚Üí Y}
    (he : IsEmbedding e) : T5Space X where
  __ := he.t1Space
  completely_normal := by
    have := he.completelyNormalSpace
    exact completely_normal

@[deprecated (since := "2024-10-26")]
alias Embedding.t5Space := IsEmbedding.t5Space

protected theorem Homeomorph.t5Space [TopologicalSpace Y] [T5Space X] (h : X ‚âÉ‚Çú Y) : T5Space Y :=
  h.symm.isClosedEmbedding.t5Space

-- see Note [lower instance priority]
/-- A `T‚ÇÖ` space is a `T‚ÇÑ` space. -/
instance (priority := 100) T5Space.toT4Space [T5Space X] : T4Space X where
  -- follows from type-class inference

/-- A subspace of a T‚ÇÖ space is a T‚ÇÖ space. -/
instance [T5Space X] {p : X ‚Üí Prop} : T5Space { x // p x } :=
  IsEmbedding.subtypeVal.t5Space

instance ULift.instT5Space [T5Space X] : T5Space (ULift X) :=
  IsEmbedding.uliftDown.t5Space

open SeparationQuotient

/-- The `SeparationQuotient` of a completely normal R‚ÇÄ space is a T‚ÇÖ space. -/
instance [CompletelyNormalSpace X] [R0Space X] : T5Space (SeparationQuotient X) where
  t1 := by
    rwa [((t1Space_TFAE (SeparationQuotient X)).out 1 0 :), SeparationQuotient.t1Space_iff]
  completely_normal s t hd‚ÇÅ hd‚ÇÇ := by
    rw [‚Üê disjoint_comap_iff surjective_mk, comap_mk_nhdsSet, comap_mk_nhdsSet]
    apply completely_normal <;> rw [‚Üê preimage_mk_closure]
    exacts [hd‚ÇÅ.preimage mk, hd‚ÇÇ.preimage mk]

end CompletelyNormal

/-- In a compact T‚ÇÇ space, the connected component of a point equals the intersection of all
its clopen neighbourhoods. -/
theorem connectedComponent_eq_iInter_isClopen [T2Space X] [CompactSpace X] (x : X) :
    connectedComponent x = ‚ãÇ s : { s : Set X // IsClopen s ‚àß x ‚àà s }, s := by
  apply Subset.antisymm connectedComponent_subset_iInter_isClopen
  -- Reduce to showing that the clopen intersection is connected.
  refine IsPreconnected.subset_connectedComponent ?_ (mem_iInter.2 fun s => s.2.2)
  -- We do this by showing that any disjoint cover by two closed sets implies
  -- that one of these closed sets must contain our whole thing.
  -- To reduce to the case where the cover is disjoint on all of `X` we need that `s` is closed
  have hs : @IsClosed X _ (‚ãÇ s : { s : Set X // IsClopen s ‚àß x ‚àà s }, s) :=
    isClosed_iInter fun s => s.2.1.1
  rw [isPreconnected_iff_subset_of_fully_disjoint_closed hs]
  intro a b ha hb hab ab_disj
  -- Since our space is normal, we get two larger disjoint open sets containing the disjoint
  -- closed sets. If we can show that our intersection is a subset of any of these we can then
  -- "descend" this to show that it is a subset of either a or b.
  rcases normal_separation ha hb ab_disj with ‚ü®u, v, hu, hv, hau, hbv, huv‚ü©
  obtain ‚ü®s, H‚ü© : ‚àÉ s : Set X, IsClopen s ‚àß x ‚àà s ‚àß s ‚äÜ u ‚à™ v := by
    /- Now we find a clopen set `s` around `x`, contained in `u ‚à™ v`. We utilize the fact that
    `X \ u ‚à™ v` will be compact, so there must be some finite intersection of clopen neighbourhoods
    of `X` disjoint to it, but a finite intersection of clopen sets is clopen,
    so we let this be our `s`. -/
    have H1 := (hu.union hv).isClosed_compl.isCompact.inter_iInter_nonempty
      (fun s : { s : Set X // IsClopen s ‚àß x ‚àà s } => s) fun s => s.2.1.1
    rw [‚Üê not_disjoint_iff_nonempty_inter, imp_not_comm, not_forall] at H1
    obtain ‚ü®si, H2‚ü© :=
      H1 (disjoint_compl_left_iff_subset.2 <| hab.trans <| union_subset_union hau hbv)
    refine ‚ü®‚ãÇ U ‚àà si, Subtype.val U, ?_, ?_, ?_‚ü©
    ¬∑ exact isClopen_biInter_finset fun s _ => s.2.1
    ¬∑ exact mem_iInter‚ÇÇ.2 fun s _ => s.2.2
    ¬∑ rwa [‚Üê disjoint_compl_left_iff_subset, disjoint_iff_inter_eq_empty,
        ‚Üê not_nonempty_iff_eq_empty]
  -- So, we get a disjoint decomposition `s = s ‚à© u ‚à™ s ‚à© v` of clopen sets. The intersection of all
  -- clopen neighbourhoods will then lie in whichever of u or v x lies in and hence will be a subset
  -- of either a or b.
  ¬∑ have H1 := isClopen_inter_of_disjoint_cover_clopen H.1 H.2.2 hu hv huv
    rw [union_comm] at H
    have H2 := isClopen_inter_of_disjoint_cover_clopen H.1 H.2.2 hv hu huv.symm
    by_cases hxu : x ‚àà u <;> [left; right]
    -- The x ‚àà u case.
    ¬∑ suffices ‚ãÇ s : { s : Set X // IsClopen s ‚àß x ‚àà s }, ‚Üës ‚äÜ u
        from Disjoint.left_le_of_le_sup_right hab (huv.mono this hbv)
      ¬∑ apply Subset.trans _ s.inter_subset_right
        exact iInter_subset (fun s : { s : Set X // IsClopen s ‚àß x ‚àà s } => s.1)
          ‚ü®s ‚à© u, H1, mem_inter H.2.1 hxu‚ü©
    -- If x ‚àâ u, we get x ‚àà v since x ‚àà u ‚à™ v. The rest is then like the x ‚àà u case.
    ¬∑ have h1 : x ‚àà v :=
        (hab.trans (union_subset_union hau hbv) (mem_iInter.2 fun i => i.2.2)).resolve_left hxu
      suffices ‚ãÇ s : { s : Set X // IsClopen s ‚àß x ‚àà s }, ‚Üës ‚äÜ v
        from (huv.symm.mono this hau).left_le_of_le_sup_left hab
      ¬∑ refine Subset.trans ?_ s.inter_subset_right
        exact iInter_subset (fun s : { s : Set X // IsClopen s ‚àß x ‚àà s } => s.1)
          ‚ü®s ‚à© v, H2, mem_inter H.2.1 h1‚ü©

/-- `ConnectedComponents X` is Hausdorff when `X` is Hausdorff and compact -/
@[stacks 0900 "The Stacks entry proves profiniteness."]
instance ConnectedComponents.t2 [T2Space X] [CompactSpace X] : T2Space (ConnectedComponents X) := by
  -- Fix 2 distinct connected components, with points a and b
  refine ‚ü®ConnectedComponents.surjective_coe.forall‚ÇÇ.2 fun a b ne => ?_‚ü©
  rw [ConnectedComponents.coe_ne_coe] at ne
  have h := connectedComponent_disjoint ne
  -- write ‚Üëb as the intersection of all clopen subsets containing it
  rw [connectedComponent_eq_iInter_isClopen b, disjoint_iff_inter_eq_empty] at h
  -- Now we show that this can be reduced to some clopen containing `‚Üëb` being disjoint to `‚Üëa`
  obtain ‚ü®U, V, hU, ha, hb, rfl‚ü© : ‚àÉ (U : Set X) (V : Set (ConnectedComponents X)),
      IsClopen U ‚àß connectedComponent a ‚à© U = ‚àÖ ‚àß connectedComponent b ‚äÜ U ‚àß (‚Üë) ‚Åª¬π' V = U := by
    have h :=
      (isClosed_connectedComponent (Œ± := X)).isCompact.elim_finite_subfamily_closed
        _ (fun s : { s : Set X // IsClopen s ‚àß b ‚àà s } => s.2.1.1) h
    obtain ‚ü®fin_a, ha‚ü© := h
    -- This clopen and its complement will separate the connected components of `a` and `b`
    set U : Set X := ‚ãÇ (i : { s // IsClopen s ‚àß b ‚àà s }) (_ : i ‚àà fin_a), i
    have hU : IsClopen U := isClopen_biInter_finset fun i _ => i.2.1
    exact ‚ü®U, (‚Üë) '' U, hU, ha, subset_iInter‚ÇÇ fun s _ => s.2.1.connectedComponent_subset s.2.2,
      (connectedComponents_preimage_image U).symm ‚ñ∏ hU.biUnion_connectedComponent_eq‚ü©
  rw [ConnectedComponents.isQuotientMap_coe.isClopen_preimage] at hU
  refine ‚ü®V·∂ú, V, hU.compl.isOpen, hU.isOpen, ?_, hb mem_connectedComponent, disjoint_compl_left‚ü©
  exact fun h => flip Set.Nonempty.ne_empty ha ‚ü®a, mem_connectedComponent, h‚ü©
