/-
Copyright (c) 2023 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Data.Real.Cardinality
import Mathlib.Topology.TietzeExtension
/-!
# Not normal topological spaces

In this file we prove (see `IsClosed.not_normal_of_continuum_le_mk`) that a separable space with a
discrete subspace of cardinality continuum is not a normal topological space.
-/

open Set Function Cardinal Topology TopologicalSpace

universe u
variable {X : Type u} [TopologicalSpace X] [SeparableSpace X]

/-- Let `s` be a closed set in a separable normal space. If the induced topology on `s` is discrete,
then `s` has cardinality less than continuum.

The proof follows
https://en.wikipedia.org/wiki/Moore_plane#Proof_that_the_Moore_plane_is_not_normal -/
theorem IsClosed.mk_lt_continuum [NormalSpace X] {s : Set X} (hs : IsClosed s)
    [DiscreteTopology s] : #s < ð”  := by
  -- Proof by contradiction: assume `ð”  â‰¤ #s`
  by_contra! h
  -- Choose a countable dense set `t : Set X`
  rcases exists_countable_dense X with âŸ¨t, htc, htdâŸ©
  haveI := htc.to_subtype
  -- To obtain a contradiction, we will prove `2 ^ ð”  â‰¤ ð” `.
  refine (Cardinal.cantor ð” ).not_ge ?_
  calc
    -- Any function `s â†’ â„` is continuous, hence `2 ^ ð”  â‰¤ #C(s, â„)`
    2 ^ ð”  â‰¤ #C(s, â„) := by
      rw [ContinuousMap.equivFnOfDiscrete.cardinal_eq, mk_arrow, mk_real, lift_continuum,
        lift_uzero]
      exact (power_le_power_left two_ne_zero h).trans (power_le_power_right (nat_lt_continuum 2).le)
    -- By the Tietze Extension Theorem, any function `f : C(s, â„)` can be extended to `C(X, â„)`,
    -- hence `#C(s, â„) â‰¤ #C(X, â„)`
    _ â‰¤ #C(X, â„) := by
      choose f hf using ContinuousMap.exists_restrict_eq (Y := â„) hs
      have hfi : Injective f := LeftInverse.injective hf
      exact mk_le_of_injective hfi
    -- Since `t` is dense, restriction `C(X, â„) â†’ C(t, â„)` is injective, hence `#C(X, â„) â‰¤ #C(t, â„)`
    _ â‰¤ #C(t, â„) := mk_le_of_injective <| ContinuousMap.injective_restrict htd
    _ â‰¤ #(t â†’ â„) := mk_le_of_injective DFunLike.coe_injective
    -- Since `t` is countable, we have `#(t â†’ â„) â‰¤ ð” `
    _ â‰¤ ð”  := by
      rw [mk_arrow, mk_real, lift_uzero, lift_continuum, continuum, â† power_mul]
      exact power_le_power_left two_ne_zero mk_le_aleph0

/-- Let `s` be a closed set in a separable space. If the induced topology on `s` is discrete and `s`
has cardinality at least continuum, then the ambient space is not a normal space. -/
theorem IsClosed.not_normal_of_continuum_le_mk {s : Set X} (hs : IsClosed s) [DiscreteTopology s]
    (hmk : ð”  â‰¤ #s) : Â¬NormalSpace X := fun _ â†¦ hs.mk_lt_continuum.not_ge hmk
