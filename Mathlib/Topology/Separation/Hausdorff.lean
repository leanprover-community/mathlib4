/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro
-/
import Mathlib.Topology.Compactness.SigmaCompact
import Mathlib.Topology.Irreducible
import Mathlib.Topology.Separation.Basic

/-!
# T‚ÇÇ and T‚ÇÇ.‚ÇÖ spaces.

This file defines the T‚ÇÇ (Hausdorff) condition, which is the most commonly-used among the various
separation axioms, and the related T‚ÇÇ.‚ÇÖ condition.

## Main definitions

* `T2Space`: A T‚ÇÇ/Hausdorff space is a space where, for every two points `x ‚â† y`,
  there is two disjoint open sets, one containing `x`, and the other `y`. T‚ÇÇ implies T‚ÇÅ and R‚ÇÅ.
* `T25Space`: A T‚ÇÇ.‚ÇÖ/Urysohn space is a space where, for every two points `x ‚â† y`,
  there is two open sets, one containing `x`, and the other `y`, whose closures are disjoint.
  T‚ÇÇ.‚ÇÖ implies T‚ÇÇ.

See `Mathlib/Topology/Separation/Regular.lean` for regular, T‚ÇÉ, etc spaces; and
`Mathlib/Topology/Separation/GDelta.lean` for the definitions of `PerfectlyNormalSpace` and
`T6Space`.

Note that `mathlib` adopts the modern convention that `m ‚â§ n` if and only if `T_m ‚Üí T_n`, but
occasionally the literature swaps definitions for e.g. T‚ÇÉ and regular.

## Main results

### T‚ÇÇ spaces

* `t2_iff_nhds`: A space is T‚ÇÇ iff the neighbourhoods of distinct points generate the bottom filter.
* `t2_iff_isClosed_diagonal`: A space is T‚ÇÇ iff the `diagonal` of `X` (that is, the set of all
  points of the form `(a, a) : X √ó X`) is closed under the product topology.
* `separatedNhds_of_finset_finset`: Any two disjoint finsets are `SeparatedNhds`.
* Most topological constructions preserve Hausdorffness;
  these results are part of the typeclass inference system (e.g. `Topology.IsEmbedding.t2Space`)
* `Set.EqOn.closure`: If two functions are equal on some set `s`, they are equal on its closure.
* `IsCompact.isClosed`: All compact sets are closed.
* `WeaklyLocallyCompactSpace.locallyCompactSpace`: If a topological space is both
  weakly locally compact (i.e., each point has a compact neighbourhood)
  and is T‚ÇÇ, then it is locally compact.
* `totallySeparatedSpace_of_t1_of_basis_clopen`: If `X` has a clopen basis, then
  it is a `TotallySeparatedSpace`.
* `loc_compact_t2_tot_disc_iff_tot_sep`: A locally compact T‚ÇÇ space is totally disconnected iff
  it is totally separated.
* `T2Quotient`: the largest T2 quotient of a given topological space.

If the space is also compact:

* `normalOfCompactT2`: A compact T‚ÇÇ space is a `NormalSpace`.
* `connectedComponent_eq_iInter_isClopen`: The connected component of a point
  is the intersection of all its clopen neighbourhoods.
* `compact_t2_tot_disc_iff_tot_sep`: Being a `TotallyDisconnectedSpace`
  is equivalent to being a `TotallySeparatedSpace`.
* `ConnectedComponents.t2`: `ConnectedComponents X` is T‚ÇÇ for `X` T‚ÇÇ and compact.

## References

* <https://en.wikipedia.org/wiki/Separation_axiom>
* [Willard's *General Topology*][zbMATH02107988]

-/

open Function Set Filter Topology TopologicalSpace

universe u v

variable {X : Type*} {Y : Type*} [TopologicalSpace X]

section Separation

/-- A T‚ÇÇ space, also known as a Hausdorff space, is one in which for every
  `x ‚â† y` there exists disjoint open sets around `x` and `y`. This is
  the most widely used of the separation axioms. -/
@[mk_iff]
class T2Space (X : Type u) [TopologicalSpace X] : Prop where
  /-- Every two points in a Hausdorff space admit disjoint open neighbourhoods. -/
  t2 : Pairwise fun x y => ‚àÉ u v : Set X, IsOpen u ‚àß IsOpen v ‚àß x ‚àà u ‚àß y ‚àà v ‚àß Disjoint u v

/-- Two different points can be separated by open sets. -/
theorem t2_separation [T2Space X] {x y : X} (h : x ‚â† y) :
    ‚àÉ u v : Set X, IsOpen u ‚àß IsOpen v ‚àß x ‚àà u ‚àß y ‚àà v ‚àß Disjoint u v :=
  T2Space.t2 h

-- todo: use this as a definition?
theorem t2Space_iff_disjoint_nhds : T2Space X ‚Üî Pairwise fun x y : X => Disjoint (ùìù x) (ùìù y) := by
  refine (t2Space_iff X).trans (forall‚ÇÉ_congr fun x y _ => ?_)
  simp only [(nhds_basis_opens x).disjoint_iff (nhds_basis_opens y), ‚Üê exists_and_left,
    and_assoc, and_comm, and_left_comm]

@[simp]
theorem disjoint_nhds_nhds [T2Space X] {x y : X} : Disjoint (ùìù x) (ùìù y) ‚Üî x ‚â† y :=
  ‚ü®fun hd he => by simp [he, nhds_neBot.ne] at hd, (t2Space_iff_disjoint_nhds.mp ‚Äπ_‚Ä∫ ¬∑)‚ü©

theorem pairwise_disjoint_nhds [T2Space X] : Pairwise (Disjoint on (ùìù : X ‚Üí Filter X)) := fun _ _ =>
  disjoint_nhds_nhds.2

protected theorem Set.pairwiseDisjoint_nhds [T2Space X] (s : Set X) : s.PairwiseDisjoint ùìù :=
  pairwise_disjoint_nhds.set_pairwise s

/-- Points of a finite set can be separated by open sets from each other. -/
theorem Set.Finite.t2_separation [T2Space X] {s : Set X} (hs : s.Finite) :
    ‚àÉ U : X ‚Üí Set X, (‚àÄ x, x ‚àà U x ‚àß IsOpen (U x)) ‚àß s.PairwiseDisjoint U :=
  s.pairwiseDisjoint_nhds.exists_mem_filter_basis hs nhds_basis_opens

-- see Note [lower instance priority]
instance (priority := 100) T2Space.t1Space [T2Space X] : T1Space X :=
  t1Space_iff_disjoint_pure_nhds.mpr fun _ _ hne =>
    (disjoint_nhds_nhds.2 hne).mono_left <| pure_le_nhds _

-- see Note [lower instance priority]
instance (priority := 100) T2Space.r1Space [T2Space X] : R1Space X :=
  ‚ü®fun x y ‚Ü¶ (eq_or_ne x y).imp specializes_of_eq disjoint_nhds_nhds.2‚ü©

theorem SeparationQuotient.t2Space_iff : T2Space (SeparationQuotient X) ‚Üî R1Space X := by
  simp only [t2Space_iff_disjoint_nhds, Pairwise, surjective_mk.forall‚ÇÇ, ne_eq, mk_eq_mk,
    r1Space_iff_inseparable_or_disjoint_nhds, ‚Üê disjoint_comap_iff surjective_mk, comap_mk_nhds_mk,
    ‚Üê or_iff_not_imp_left]

instance SeparationQuotient.t2Space [R1Space X] : T2Space (SeparationQuotient X) :=
  t2Space_iff.2 ‚Äπ_‚Ä∫

instance (priority := 80) [R1Space X] [T0Space X] : T2Space X :=
  t2Space_iff_disjoint_nhds.2 fun _x _y hne ‚Ü¶ disjoint_nhds_nhds_iff_not_inseparable.2 fun hxy ‚Ü¶
    hne hxy.eq

theorem R1Space.t2Space_iff_t0Space [R1Space X] : T2Space X ‚Üî T0Space X := by
  constructor <;> intro <;> infer_instance

/-- A space is T‚ÇÇ iff the neighbourhoods of distinct points generate the bottom filter. -/
theorem t2_iff_nhds : T2Space X ‚Üî ‚àÄ {x y : X}, NeBot (ùìù x ‚äì ùìù y) ‚Üí x = y := by
  simp only [t2Space_iff_disjoint_nhds, disjoint_iff, neBot_iff, Ne, not_imp_comm, Pairwise]

theorem eq_of_nhds_neBot [T2Space X] {x y : X} (h : NeBot (ùìù x ‚äì ùìù y)) : x = y :=
  t2_iff_nhds.mp ‚Äπ_‚Ä∫ h

theorem t2Space_iff_nhds :
    T2Space X ‚Üî Pairwise fun x y : X => ‚àÉ U ‚àà ùìù x, ‚àÉ V ‚àà ùìù y, Disjoint U V := by
  simp only [t2Space_iff_disjoint_nhds, Filter.disjoint_iff, Pairwise]

theorem t2_separation_nhds [T2Space X] {x y : X} (h : x ‚â† y) :
    ‚àÉ u v, u ‚àà ùìù x ‚àß v ‚àà ùìù y ‚àß Disjoint u v :=
  let ‚ü®u, v, open_u, open_v, x_in, y_in, huv‚ü© := t2_separation h
  ‚ü®u, v, open_u.mem_nhds x_in, open_v.mem_nhds y_in, huv‚ü©

theorem t2_separation_compact_nhds [LocallyCompactSpace X] [T2Space X] {x y : X} (h : x ‚â† y) :
    ‚àÉ u v, u ‚àà ùìù x ‚àß v ‚àà ùìù y ‚àß IsCompact u ‚àß IsCompact v ‚àß Disjoint u v := by
  simpa only [exists_prop, ‚Üê exists_and_left, and_comm, and_assoc, and_left_comm] using
    ((compact_basis_nhds x).disjoint_iff (compact_basis_nhds y)).1 (disjoint_nhds_nhds.2 h)

theorem t2_iff_ultrafilter :
    T2Space X ‚Üî ‚àÄ {x y : X} (f : Ultrafilter X), ‚Üëf ‚â§ ùìù x ‚Üí ‚Üëf ‚â§ ùìù y ‚Üí x = y :=
  t2_iff_nhds.trans <| by simp only [‚Üê exists_ultrafilter_iff, and_imp, le_inf_iff, exists_imp]

theorem t2_iff_isClosed_diagonal : T2Space X ‚Üî IsClosed (diagonal X) := by
  simp only [t2Space_iff_disjoint_nhds, ‚Üê isOpen_compl_iff, isOpen_iff_mem_nhds, Prod.forall,
    nhds_prod_eq, compl_diagonal_mem_prod, mem_compl_iff, mem_diagonal_iff, Pairwise]

theorem isClosed_diagonal [T2Space X] : IsClosed (diagonal X) :=
  t2_iff_isClosed_diagonal.mp ‚Äπ_‚Ä∫

theorem tendsto_nhds_unique [T2Space X] {f : Y ‚Üí X} {l : Filter Y} {a b : X} [NeBot l]
    (ha : Tendsto f l (ùìù a)) (hb : Tendsto f l (ùìù b)) : a = b :=
  (tendsto_nhds_unique_inseparable ha hb).eq

theorem tendsto_nhds_unique' [T2Space X] {f : Y ‚Üí X} {l : Filter Y} {a b : X} (_ : NeBot l)
    (ha : Tendsto f l (ùìù a)) (hb : Tendsto f l (ùìù b)) : a = b :=
  tendsto_nhds_unique ha hb

theorem tendsto_nhds_unique_of_eventuallyEq [T2Space X] {f g : Y ‚Üí X} {l : Filter Y} {a b : X}
    [NeBot l] (ha : Tendsto f l (ùìù a)) (hb : Tendsto g l (ùìù b)) (hfg : f =·∂†[l] g) : a = b :=
  tendsto_nhds_unique (ha.congr' hfg) hb

theorem tendsto_nhds_unique_of_frequently_eq [T2Space X] {f g : Y ‚Üí X} {l : Filter Y} {a b : X}
    (ha : Tendsto f l (ùìù a)) (hb : Tendsto g l (ùìù b)) (hfg : ‚àÉ·∂† x in l, f x = g x) : a = b :=
  have : ‚àÉ·∂† z : X √ó X in ùìù (a, b), z.1 = z.2 := (ha.prodMk_nhds hb).frequently hfg
  not_not.1 fun hne => this (isClosed_diagonal.isOpen_compl.mem_nhds hne)

/-- If `s` and `t` are compact sets in a T‚ÇÇ space, then the set neighborhoods filter of `s ‚à© t`
is the infimum of set neighborhoods filters for `s` and `t`.

For general sets, only the `‚â§` inequality holds, see `nhdsSet_inter_le`. -/
theorem IsCompact.nhdsSet_inter_eq [T2Space X] {s t : Set X} (hs : IsCompact s) (ht : IsCompact t) :
    ùìùÀ¢ (s ‚à© t) = ùìùÀ¢ s ‚äì ùìùÀ¢ t := by
  refine le_antisymm (nhdsSet_inter_le _ _) ?_
  simp_rw [hs.nhdsSet_inf_eq_biSup, ht.inf_nhdsSet_eq_biSup, nhdsSet, sSup_image]
  refine iSup‚ÇÇ_le fun x hxs ‚Ü¶ iSup‚ÇÇ_le fun y hyt ‚Ü¶ ?_
  rcases eq_or_ne x y with (rfl | hne)
  ¬∑ exact le_iSup‚ÇÇ_of_le x ‚ü®hxs, hyt‚ü© (inf_idem _).le
  ¬∑ exact (disjoint_nhds_nhds.mpr hne).eq_bot ‚ñ∏ bot_le

/-- In a `T2Space X`, for a compact set `t` and a point `x` outside `t`, there are open sets `U`,
`V` that separate `t` and `x`. -/
lemma IsCompact.separation_of_notMem {X : Type u_1} [TopologicalSpace X] [T2Space X] {x : X}
    {t : Set X} (H1 : IsCompact t) (H2 : x ‚àâ t) :
    ‚àÉ (U : Set X), ‚àÉ (V : Set X), IsOpen U ‚àß IsOpen V ‚àß t ‚äÜ U ‚àß x ‚àà V ‚àß Disjoint U V := by
  simpa [SeparatedNhds] using SeparatedNhds.of_isCompact_isCompact_isClosed H1 isCompact_singleton
    isClosed_singleton <| disjoint_singleton_right.mpr H2

@[deprecated (since := "2025-05-23")]
alias IsCompact.separation_of_not_mem := IsCompact.separation_of_notMem

/-- In a `T2Space X`, for a compact set `t` and a point `x` outside `t`, `ùìùÀ¢ t` and `ùìù x` are
disjoint. -/
lemma IsCompact.disjoint_nhdsSet_nhds {X : Type u_1} [TopologicalSpace X] [T2Space X] {x : X}
    {t : Set X} (H1 : IsCompact t) (H2 : x ‚àâ t) :
    Disjoint (ùìùÀ¢ t) (ùìù x) := by
  simpa using SeparatedNhds.disjoint_nhdsSet <| .of_isCompact_isCompact_isClosed H1
    isCompact_singleton isClosed_singleton <| disjoint_singleton_right.mpr H2

/-- If a function `f` is

- injective on a compact set `s`;
- continuous at every point of this set;
- injective on a neighborhood of each point of this set,

then it is injective on a neighborhood of this set. -/
theorem Set.InjOn.exists_mem_nhdsSet {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    [T2Space Y] {f : X ‚Üí Y} {s : Set X} (inj : InjOn f s) (sc : IsCompact s)
    (fc : ‚àÄ x ‚àà s, ContinuousAt f x) (loc : ‚àÄ x ‚àà s, ‚àÉ u ‚àà ùìù x, InjOn f u) :
    ‚àÉ t ‚àà ùìùÀ¢ s, InjOn f t := by
  have : ‚àÄ x ‚àà s √óÀ¢ s, ‚àÄ·∂† y in ùìù x, f y.1 = f y.2 ‚Üí y.1 = y.2 := fun (x, y) ‚ü®hx, hy‚ü© ‚Ü¶ by
    rcases eq_or_ne x y with rfl | hne
    ¬∑ rcases loc x hx with ‚ü®u, hu, hf‚ü©
      exact Filter.mem_of_superset (prod_mem_nhds hu hu) <| forall_prod_set.2 hf
    ¬∑ suffices ‚àÄ·∂† z in ùìù (x, y), f z.1 ‚â† f z.2 from this.mono fun _ hne h ‚Ü¶ absurd h hne
      refine (fc x hx).prodMap' (fc y hy) <| isClosed_diagonal.isOpen_compl.mem_nhds ?_
      exact inj.ne hx hy hne
  rw [‚Üê eventually_nhdsSet_iff_forall, sc.nhdsSet_prod_eq sc] at this
  exact eventually_prod_self_iff.1 this

/-- If a function `f` is

- injective on a compact set `s`;
- continuous at every point of this set;
- injective on a neighborhood of each point of this set,

then it is injective on an open neighborhood of this set. -/
theorem Set.InjOn.exists_isOpen_superset {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    [T2Space Y] {f : X ‚Üí Y} {s : Set X} (inj : InjOn f s) (sc : IsCompact s)
    (fc : ‚àÄ x ‚àà s, ContinuousAt f x) (loc : ‚àÄ x ‚àà s, ‚àÉ u ‚àà ùìù x, InjOn f u) :
    ‚àÉ t, IsOpen t ‚àß s ‚äÜ t ‚àß InjOn f t :=
  let ‚ü®_t, hst, ht‚ü© := inj.exists_mem_nhdsSet sc fc loc
  let ‚ü®u, huo, hsu, hut‚ü© := mem_nhdsSet_iff_exists.1 hst
  ‚ü®u, huo, hsu, ht.mono hut‚ü©

section limUnder

variable [T2Space X] {f : Filter X}

/-!
### Properties of `lim` and `limUnder`

In this section we use explicit `Nonempty X` instances for `lim` and `limUnder`. This way the lemmas
are useful without a `Nonempty X` instance.
-/


theorem lim_eq {x : X} [NeBot f] (h : f ‚â§ ùìù x) : @lim _ _ ‚ü®x‚ü© f = x :=
  tendsto_nhds_unique (le_nhds_lim ‚ü®x, h‚ü©) h

theorem lim_eq_iff [NeBot f] (h : ‚àÉ x : X, f ‚â§ ùìù x) {x} : @lim _ _ ‚ü®x‚ü© f = x ‚Üî f ‚â§ ùìù x :=
  ‚ü®fun c => c ‚ñ∏ le_nhds_lim h, lim_eq‚ü©

theorem Ultrafilter.lim_eq_iff_le_nhds [CompactSpace X] {x : X} {F : Ultrafilter X} :
    F.lim = x ‚Üî ‚ÜëF ‚â§ ùìù x :=
  ‚ü®fun h => h ‚ñ∏ F.le_nhds_lim, lim_eq‚ü©

theorem isOpen_iff_ultrafilter' [CompactSpace X] (U : Set X) :
    IsOpen U ‚Üî ‚àÄ F : Ultrafilter X, F.lim ‚àà U ‚Üí U ‚àà F.1 := by
  rw [isOpen_iff_ultrafilter]
  refine ‚ü®fun h F hF => h F.lim hF F F.le_nhds_lim, ?_‚ü©
  intro cond x hx f h
  rw [‚Üê Ultrafilter.lim_eq_iff_le_nhds.2 h] at hx
  exact cond _ hx

theorem Filter.Tendsto.limUnder_eq {x : X} {f : Filter Y} [NeBot f] {g : Y ‚Üí X}
    (h : Tendsto g f (ùìù x)) : @limUnder _ _ _ ‚ü®x‚ü© f g = x :=
  lim_eq h

theorem Filter.limUnder_eq_iff {f : Filter Y} [NeBot f] {g : Y ‚Üí X} (h : ‚àÉ x, Tendsto g f (ùìù x))
    {x} : @limUnder _ _ _ ‚ü®x‚ü© f g = x ‚Üî Tendsto g f (ùìù x) :=
  ‚ü®fun c => c ‚ñ∏ tendsto_nhds_limUnder h, Filter.Tendsto.limUnder_eq‚ü©

theorem Continuous.limUnder_eq [TopologicalSpace Y] {f : Y ‚Üí X} (h : Continuous f) (y : Y) :
    @limUnder _ _ _ ‚ü®f y‚ü© (ùìù y) f = f y :=
  (h.tendsto y).limUnder_eq

@[simp]
theorem lim_nhds (x : X) : @lim _ _ ‚ü®x‚ü© (ùìù x) = x :=
  lim_eq le_rfl

@[simp]
theorem limUnder_nhds_id (x : X) : @limUnder _ _ _ ‚ü®x‚ü© (ùìù x) id = x :=
  lim_nhds x

@[simp]
theorem lim_nhdsWithin {x : X} {s : Set X} (h : x ‚àà closure s) : @lim _ _ ‚ü®x‚ü© (ùìù[s] x) = x :=
  haveI : NeBot (ùìù[s] x) := mem_closure_iff_clusterPt.1 h
  lim_eq inf_le_left

@[simp]
theorem limUnder_nhdsWithin_id {x : X} {s : Set X} (h : x ‚àà closure s) :
    @limUnder _ _ _ ‚ü®x‚ü© (ùìù[s] x) id = x :=
  lim_nhdsWithin h

end limUnder

/-!
### `T2Space` constructions

We use two lemmas to prove that various standard constructions generate Hausdorff spaces from
Hausdorff spaces:

* `separated_by_continuous` says that two points `x y : X` can be separated by open neighborhoods
  provided that there exists a continuous map `f : X ‚Üí Y` with a Hausdorff codomain such that
  `f x ‚â† f y`. We use this lemma to prove that topological spaces defined using `induced` are
  Hausdorff spaces.

* `separated_by_isOpenEmbedding` says that for an open embedding `f : X ‚Üí Y` of a Hausdorff space
  `X`, the images of two distinct points `x y : X`, `x ‚â† y` can be separated by open neighborhoods.
  We use this lemma to prove that topological spaces defined using `coinduced` are Hausdorff spaces.
-/

-- see Note [lower instance priority]
instance (priority := 100) DiscreteTopology.toT2Space
    [DiscreteTopology X] : T2Space X :=
  ‚ü®fun x y h => ‚ü®{x}, {y}, isOpen_discrete _, isOpen_discrete _, rfl, rfl, disjoint_singleton.2 h‚ü©‚ü©

theorem separated_by_continuous [TopologicalSpace Y] [T2Space Y]
    {f : X ‚Üí Y} (hf : Continuous f) {x y : X} (h : f x ‚â† f y) :
    ‚àÉ u v : Set X, IsOpen u ‚àß IsOpen v ‚àß x ‚àà u ‚àß y ‚àà v ‚àß Disjoint u v :=
  let ‚ü®u, v, uo, vo, xu, yv, uv‚ü© := t2_separation h
  ‚ü®f ‚Åª¬π' u, f ‚Åª¬π' v, uo.preimage hf, vo.preimage hf, xu, yv, uv.preimage _‚ü©

theorem separated_by_isOpenEmbedding [TopologicalSpace Y] [T2Space X]
    {f : X ‚Üí Y} (hf : IsOpenEmbedding f) {x y : X} (h : x ‚â† y) :
    ‚àÉ u v : Set Y, IsOpen u ‚àß IsOpen v ‚àß f x ‚àà u ‚àß f y ‚àà v ‚àß Disjoint u v :=
  let ‚ü®u, v, uo, vo, xu, yv, uv‚ü© := t2_separation h
  ‚ü®f '' u, f '' v, hf.isOpenMap _ uo, hf.isOpenMap _ vo, mem_image_of_mem _ xu,
    mem_image_of_mem _ yv, disjoint_image_of_injective hf.injective uv‚ü©

instance {p : X ‚Üí Prop} [T2Space X] : T2Space (Subtype p) := inferInstance

instance Prod.t2Space [T2Space X] [TopologicalSpace Y] [T2Space Y] : T2Space (X √ó Y) :=
  inferInstance

/-- If the codomain of an injective continuous function is a Hausdorff space, then so is its
domain. -/
theorem T2Space.of_injective_continuous [TopologicalSpace Y] [T2Space Y] {f : X ‚Üí Y}
    (hinj : Injective f) (hc : Continuous f) : T2Space X :=
  ‚ü®fun _ _ h => separated_by_continuous hc (hinj.ne h)‚ü©

/-- If the codomain of a topological embedding is a Hausdorff space, then so is its domain.
See also `T2Space.of_continuous_injective`. -/
theorem Topology.IsEmbedding.t2Space [TopologicalSpace Y] [T2Space Y] {f : X ‚Üí Y}
    (hf : IsEmbedding f) : T2Space X :=
  .of_injective_continuous hf.injective hf.continuous

protected theorem Homeomorph.t2Space [TopologicalSpace Y] [T2Space X] (h : X ‚âÉ‚Çú Y) : T2Space Y :=
  h.symm.isEmbedding.t2Space

instance ULift.instT2Space [T2Space X] : T2Space (ULift X) :=
  IsEmbedding.uliftDown.t2Space

instance [T2Space X] [TopologicalSpace Y] [T2Space Y] :
    T2Space (X ‚äï Y) := by
  constructor
  rintro (x | x) (y | y) h
  ¬∑ exact separated_by_isOpenEmbedding .inl <| ne_of_apply_ne _ h
  ¬∑ exact separated_by_continuous continuous_isLeft <| by simp
  ¬∑ exact separated_by_continuous continuous_isLeft <| by simp
  ¬∑ exact separated_by_isOpenEmbedding .inr <| ne_of_apply_ne _ h

instance Pi.t2Space {Y : X ‚Üí Type v} [‚àÄ a, TopologicalSpace (Y a)]
    [‚àÄ a, T2Space (Y a)] : T2Space (‚àÄ a, Y a) :=
  inferInstance

instance Sigma.t2Space {Œπ} {X : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (X i)] [‚àÄ a, T2Space (X a)] :
    T2Space (Œ£ i, X i) := by
  constructor
  rintro ‚ü®i, x‚ü© ‚ü®j, y‚ü© neq
  rcases eq_or_ne i j with (rfl | h)
  ¬∑ replace neq : x ‚â† y := ne_of_apply_ne _ neq
    exact separated_by_isOpenEmbedding .sigmaMk neq
  ¬∑ let _ := (‚ä• : TopologicalSpace Œπ); have : DiscreteTopology Œπ := ‚ü®rfl‚ü©
    exact separated_by_continuous (continuous_def.2 fun u _ => isOpen_sigma_fst_preimage u) h

section
variable (X)

/-- The smallest equivalence relation on a topological space giving a T2 quotient. -/
def t2Setoid : Setoid X := sInf {s | T2Space (Quotient s)}

/-- The largest T2 quotient of a topological space. This construction is left-adjoint to the
inclusion of T2 spaces into all topological spaces. -/
def T2Quotient := Quotient (t2Setoid X)

@[deprecated (since := "2025-05-15")] alias t2Quotient := T2Quotient

namespace T2Quotient
variable {X}

instance : TopologicalSpace (T2Quotient X) :=
  inferInstanceAs <| TopologicalSpace (Quotient _)

/-- The map from a topological space to its largest T2 quotient. -/
def mk : X ‚Üí T2Quotient X := Quotient.mk (t2Setoid X)

lemma mk_eq {x y : X} : mk x = mk y ‚Üî ‚àÄ s : Setoid X, T2Space (Quotient s) ‚Üí s x y :=
  Setoid.quotient_mk_sInf_eq

variable (X)

lemma surjective_mk : Surjective (mk : X ‚Üí T2Quotient X) := Quotient.mk_surjective

lemma continuous_mk : Continuous (mk : X ‚Üí T2Quotient X) :=
  continuous_quotient_mk'

variable {X}

@[elab_as_elim]
protected lemma inductionOn {motive : T2Quotient X ‚Üí Prop} (q : T2Quotient X)
    (h : ‚àÄ x, motive (T2Quotient.mk x)) : motive q := Quotient.inductionOn q h

@[elab_as_elim]
protected lemma inductionOn‚ÇÇ [TopologicalSpace Y] {motive : T2Quotient X ‚Üí T2Quotient Y ‚Üí Prop}
    (q : T2Quotient X) (q' : T2Quotient Y) (h : ‚àÄ x y, motive (mk x) (mk y)) : motive q q' :=
  Quotient.inductionOn‚ÇÇ q q' h

/-- The largest T2 quotient of a topological space is indeed T2. -/
instance : T2Space (T2Quotient X) := by
  rw [t2Space_iff]
  rintro ‚ü®x‚ü© ‚ü®y‚ü© (h : ¬¨ T2Quotient.mk x = T2Quotient.mk y)
  obtain ‚ü®s, hs, hsxy‚ü© : ‚àÉ s, T2Space (Quotient s) ‚àß Quotient.mk s x ‚â† Quotient.mk s y := by
    simpa [T2Quotient.mk_eq] using h
  exact separated_by_continuous (continuous_map_sInf (by exact hs)) hsxy

lemma compatible {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] [T2Space Y]
    {f : X ‚Üí Y} (hf : Continuous f) : letI _ := t2Setoid X
    ‚àÄ (a b : X), a ‚âà b ‚Üí f a = f b := by
  change t2Setoid X ‚â§ Setoid.ker f
  exact sInf_le <| .of_injective_continuous
    (Setoid.ker_lift_injective _) (hf.quotient_lift fun _ _ ‚Ü¶ id)

/-- The universal property of the largest T2 quotient of a topological space `X`: any continuous
map from `X` to a T2 space `Y` uniquely factors through `T2Quotient X`. This declaration builds the
factored map. Its continuity is `T2Quotient.continuous_lift`, the fact that it indeed factors the
original map is `T2Quotient.lift_mk` and uniqueness is `T2Quotient.unique_lift`. -/
def lift {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] [T2Space Y]
    {f : X ‚Üí Y} (hf : Continuous f) : T2Quotient X ‚Üí Y :=
  Quotient.lift f (T2Quotient.compatible hf)

lemma continuous_lift {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] [T2Space Y]
    {f : X ‚Üí Y} (hf : Continuous f) : Continuous (T2Quotient.lift hf) :=
  continuous_coinduced_dom.mpr hf

@[simp]
lemma lift_mk {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] [T2Space Y]
    {f : X ‚Üí Y} (hf : Continuous f) (x : X) : lift hf (mk x) = f x :=
  Quotient.lift_mk (s := t2Setoid X) f (T2Quotient.compatible hf) x

lemma unique_lift {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] [T2Space Y]
    {f : X ‚Üí Y} (hf : Continuous f) {g : T2Quotient X ‚Üí Y} (hfg : g ‚àò mk = f) :
    g = lift hf := by
  apply surjective_mk X |>.right_cancellable |>.mp <| funext _
  simp [‚Üê hfg]

end T2Quotient
end

variable {Z : Type*} [TopologicalSpace Y] [TopologicalSpace Z]

theorem isClosed_eq [T2Space X] {f g : Y ‚Üí X} (hf : Continuous f) (hg : Continuous g) :
    IsClosed { y : Y | f y = g y } :=
  continuous_iff_isClosed.mp (hf.prodMk hg) _ isClosed_diagonal

/-- If functions `f` and `g` are continuous on a closed set `s`,
then the set of points `x ‚àà s` such that `f x = g x` is a closed set. -/
protected theorem IsClosed.isClosed_eq [T2Space Y] {f g : X ‚Üí Y} {s : Set X} (hs : IsClosed s)
    (hf : ContinuousOn f s) (hg : ContinuousOn g s) : IsClosed {x ‚àà s | f x = g x} :=
  (hf.prodMk hg).preimage_isClosed_of_isClosed hs isClosed_diagonal

theorem isOpen_ne_fun [T2Space X] {f g : Y ‚Üí X} (hf : Continuous f) (hg : Continuous g) :
    IsOpen { y : Y | f y ‚â† g y } :=
  isOpen_compl_iff.mpr <| isClosed_eq hf hg

/-- If two continuous maps are equal on `s`, then they are equal on the closure of `s`. See also
`Set.EqOn.of_subset_closure` for a more general version. -/
protected theorem Set.EqOn.closure [T2Space X] {s : Set Y} {f g : Y ‚Üí X} (h : EqOn f g s)
    (hf : Continuous f) (hg : Continuous g) : EqOn f g (closure s) :=
  closure_minimal h (isClosed_eq hf hg)

/-- If two continuous functions are equal on a dense set, then they are equal. -/
theorem Continuous.ext_on [T2Space X] {s : Set Y} (hs : Dense s) {f g : Y ‚Üí X} (hf : Continuous f)
    (hg : Continuous g) (h : EqOn f g s) : f = g :=
  funext fun x => h.closure hf hg (hs x)

theorem eqOn_closure‚ÇÇ' [T2Space Z] {s : Set X} {t : Set Y} {f g : X ‚Üí Y ‚Üí Z}
    (h : ‚àÄ x ‚àà s, ‚àÄ y ‚àà t, f x y = g x y) (hf‚ÇÅ : ‚àÄ x, Continuous (f x))
    (hf‚ÇÇ : ‚àÄ y, Continuous fun x => f x y) (hg‚ÇÅ : ‚àÄ x, Continuous (g x))
    (hg‚ÇÇ : ‚àÄ y, Continuous fun x => g x y) : ‚àÄ x ‚àà closure s, ‚àÄ y ‚àà closure t, f x y = g x y :=
  suffices closure s ‚äÜ ‚ãÇ y ‚àà closure t, { x | f x y = g x y } by simpa only [subset_def, mem_iInter]
  (closure_minimal fun x hx => mem_iInter‚ÇÇ.2 <| Set.EqOn.closure (h x hx) (hf‚ÇÅ _) (hg‚ÇÅ _)) <|
    isClosed_biInter fun _ _ => isClosed_eq (hf‚ÇÇ _) (hg‚ÇÇ _)

theorem eqOn_closure‚ÇÇ [T2Space Z] {s : Set X} {t : Set Y} {f g : X ‚Üí Y ‚Üí Z}
    (h : ‚àÄ x ‚àà s, ‚àÄ y ‚àà t, f x y = g x y) (hf : Continuous (uncurry f))
    (hg : Continuous (uncurry g)) : ‚àÄ x ‚àà closure s, ‚àÄ y ‚àà closure t, f x y = g x y :=
  eqOn_closure‚ÇÇ' h hf.uncurry_left hf.uncurry_right hg.uncurry_left hg.uncurry_right

/-- If `f x = g x` for all `x ‚àà s` and `f`, `g` are continuous on `t`, `s ‚äÜ t ‚äÜ closure s`, then
`f x = g x` for all `x ‚àà t`. See also `Set.EqOn.closure`. -/
theorem Set.EqOn.of_subset_closure [T2Space Y] {s t : Set X} {f g : X ‚Üí Y} (h : EqOn f g s)
    (hf : ContinuousOn f t) (hg : ContinuousOn g t) (hst : s ‚äÜ t) (hts : t ‚äÜ closure s) :
    EqOn f g t := by
  intro x hx
  have : (ùìù[s] x).NeBot := mem_closure_iff_clusterPt.mp (hts hx)
  exact
    tendsto_nhds_unique_of_eventuallyEq ((hf x hx).mono_left <| nhdsWithin_mono _ hst)
      ((hg x hx).mono_left <| nhdsWithin_mono _ hst) (h.eventuallyEq_of_mem self_mem_nhdsWithin)

theorem Function.LeftInverse.isClosed_range [T2Space X] {f : X ‚Üí Y} {g : Y ‚Üí X}
    (h : Function.LeftInverse f g) (hf : Continuous f) (hg : Continuous g) : IsClosed (range g) :=
  have : EqOn (g ‚àò f) id (closure <| range g) :=
    h.rightInvOn_range.eqOn.closure (hg.comp hf) continuous_id
  isClosed_of_closure_subset fun x hx => ‚ü®f x, this hx‚ü©

theorem Function.LeftInverse.isClosedEmbedding [T2Space X] {f : X ‚Üí Y} {g : Y ‚Üí X}
    (h : Function.LeftInverse f g) (hf : Continuous f) (hg : Continuous g) : IsClosedEmbedding g :=
  ‚ü®.of_leftInverse h hf hg, h.isClosed_range hf hg‚ü©

theorem SeparatedNhds.of_isCompact_isCompact [T2Space X] {s t : Set X} (hs : IsCompact s)
    (ht : IsCompact t) (hst : Disjoint s t) : SeparatedNhds s t := by
  simp only [SeparatedNhds, prod_subset_compl_diagonal_iff_disjoint.symm] at hst ‚ä¢
  exact generalized_tube_lemma hs ht isClosed_diagonal.isOpen_compl hst

/-- In a `T2Space X`, for disjoint closed sets `s t` such that `closure s·∂ú` is compact,
there are neighbourhoods that separate `s` and `t`. -/
lemma SeparatedNhds.of_isClosed_isCompact_closure_compl_isClosed [T2Space X] {s : Set X}
    {t : Set X} (H1 : IsClosed s) (H2 : IsCompact (closure s·∂ú)) (H3 : IsClosed t)
    (H4 : Disjoint s t) : SeparatedNhds s t := by
  -- Since `t` is a closed subset of the compact set `closure s·∂ú`, it is compact.
  have ht : IsCompact t := .of_isClosed_subset H2 H3 <| H4.subset_compl_left.trans subset_closure
  -- we split `s` into its frontier and its interior.
  rw [‚Üê diff_union_of_subset (interior_subset (s := s))]
  -- since `t ‚äÜ s·∂ú`, which is open, and `interior s` is open, we have
  -- `SeparatedNhds (interior s) t`, which leaves us only with the frontier.
  refine .union_left ?_ ‚ü®interior s, s·∂ú, isOpen_interior, H1.isOpen_compl, le_rfl,
    H4.subset_compl_left, disjoint_compl_right.mono_left interior_subset‚ü©
  -- Since the frontier of `s` is compact (as it is a subset of `closure s·∂ú`), we simply apply
  -- `SeparatedNhds_of_isCompact_isCompact`.
  rw [‚Üê H1.frontier_eq, frontier_eq_closure_inter_closure, H1.closure_eq]
  refine .of_isCompact_isCompact ?_ ht (disjoint_of_subset_left inter_subset_left H4)
  exact H2.of_isClosed_subset (H1.inter isClosed_closure) inter_subset_right

section SeparatedFinset

theorem SeparatedNhds.of_finset_finset [T2Space X] (s t : Finset X) (h : Disjoint s t) :
    SeparatedNhds (s : Set X) t :=
  .of_isCompact_isCompact s.finite_toSet.isCompact t.finite_toSet.isCompact <| mod_cast h

theorem SeparatedNhds.of_singleton_finset [T2Space X] {x : X} {s : Finset X} (h : x ‚àâ s) :
    SeparatedNhds ({x} : Set X) s :=
  mod_cast .of_finset_finset {x} s (Finset.disjoint_singleton_left.mpr h)

end SeparatedFinset

/-- In a `T2Space`, every compact set is closed. -/
theorem IsCompact.isClosed [T2Space X] {s : Set X} (hs : IsCompact s) : IsClosed s :=
  isClosed_iff_forall_filter.2 fun _x _f _ hfs hfx =>
    let ‚ü®_y, hy, hfy‚ü© := hs.exists_clusterPt hfs
    mem_of_eq_of_mem (eq_of_nhds_neBot (hfy.mono hfx).neBot).symm hy

theorem IsCompact.preimage_continuous [CompactSpace X] [T2Space Y] {f : X ‚Üí Y} {s : Set Y}
    (hs : IsCompact s) (hf : Continuous f) : IsCompact (f ‚Åª¬π' s) :=
  (hs.isClosed.preimage hf).isCompact

lemma Pi.isCompact_iff {Œπ : Type*} {X : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (X i)]
    [‚àÄ i, T2Space (X i)] {s : Set (Œ† i, X i)} :
    IsCompact s ‚Üî IsClosed s ‚àß ‚àÄ i, IsCompact (eval i '' s) := by
  constructor <;> intro H
  ¬∑ exact ‚ü®H.isClosed, fun i ‚Ü¶ H.image <| continuous_apply i‚ü©
  ¬∑ exact IsCompact.of_isClosed_subset (isCompact_univ_pi H.2) H.1 (subset_pi_eval_image univ s)

lemma Pi.isCompact_closure_iff {Œπ : Type*} {X : Œπ ‚Üí Type*} [‚àÄ i, TopologicalSpace (X i)]
    [‚àÄ i, T2Space (X i)] {s : Set (Œ† i, X i)} :
    IsCompact (closure s) ‚Üî ‚àÄ i, IsCompact (closure <| eval i '' s) := by
  simp_rw [‚Üê exists_isCompact_superset_iff, Pi.exists_compact_superset_iff, image_subset_iff]

/-- If `V : Œπ ‚Üí Set X` is a decreasing family of compact sets then any neighborhood of
`‚ãÇ i, V i` contains some `V i`. This is a version of `exists_subset_nhds_of_isCompact'` where we
don't need to assume each `V i` closed because it follows from compactness since `X` is
assumed to be Hausdorff. -/
theorem exists_subset_nhds_of_isCompact [T2Space X] {Œπ : Type*} [Nonempty Œπ] {V : Œπ ‚Üí Set X}
    (hV : Directed (¬∑ ‚äá ¬∑) V) (hV_cpct : ‚àÄ i, IsCompact (V i)) {U : Set X}
    (hU : ‚àÄ x ‚àà ‚ãÇ i, V i, U ‚àà ùìù x) : ‚àÉ i, V i ‚äÜ U :=
  exists_subset_nhds_of_isCompact' hV hV_cpct (fun i => (hV_cpct i).isClosed) hU

theorem CompactExhaustion.isClosed [T2Space X] (K : CompactExhaustion X) (n : ‚Ñï) : IsClosed (K n) :=
  (K.isCompact n).isClosed

theorem IsCompact.inter [T2Space X] {s t : Set X} (hs : IsCompact s) (ht : IsCompact t) :
    IsCompact (s ‚à© t) :=
  hs.inter_right <| ht.isClosed

theorem image_closure_of_isCompact [T2Space Y] {s : Set X} (hs : IsCompact (closure s)) {f : X ‚Üí Y}
    (hf : ContinuousOn f (closure s)) : f '' closure s = closure (f '' s) :=
  Subset.antisymm hf.image_closure <|
    closure_minimal (image_mono subset_closure) (hs.image_of_continuousOn hf).isClosed

/-- Two continuous maps into a Hausdorff space agree at a point iff they agree in a
neighborhood. -/
theorem ContinuousAt.ne_iff_eventually_ne [T2Space Y] {x : X} {f g : X ‚Üí Y}
    (hf : ContinuousAt f x) (hg : ContinuousAt g x) :
    f x ‚â† g x ‚Üî ‚àÄ·∂† x in ùìù x, f x ‚â† g x := by
  constructor <;> intro hfg
  ¬∑ obtain ‚ü®Uf, Ug, h‚ÇÅU, h‚ÇÇU, h‚ÇÉU, h‚ÇÑU, h‚ÇÖU‚ü© := t2_separation hfg
    rw [Set.disjoint_iff_inter_eq_empty] at h‚ÇÖU
    filter_upwards [inter_mem
      (hf.preimage_mem_nhds (IsOpen.mem_nhds h‚ÇÅU h‚ÇÉU))
      (hg.preimage_mem_nhds (IsOpen.mem_nhds h‚ÇÇU h‚ÇÑU))]
    intro x hx
    simp only [Set.mem_inter_iff, Set.mem_preimage] at hx
    by_contra H
    rw [H] at hx
    have : g x ‚àà Uf ‚à© Ug := hx
    simp [h‚ÇÖU] at this
  ¬∑ obtain ‚ü®t, h‚ÇÅt, h‚ÇÇt, h‚ÇÉt‚ü© := eventually_nhds_iff.1 hfg
    exact h‚ÇÅt x h‚ÇÉt

/-- **Local identity principle** for continuous maps: Two continuous maps into a Hausdorff space
agree in a punctured neighborhood of a non-isolated point iff they agree in a neighborhood. -/
theorem ContinuousAt.eventuallyEq_nhds_iff_eventuallyEq_nhdsNE [T2Space Y] {x : X} {f g : X ‚Üí Y}
    (hf : ContinuousAt f x) (hg : ContinuousAt g x) [(ùìù[‚â†] x).NeBot] :
    f =·∂†[ùìù[‚â†] x] g ‚Üî f =·∂†[ùìù x] g := by
  constructor <;> intro hfg
  ¬∑ apply eventuallyEq_nhds_of_eventuallyEq_nhdsNE hfg
    by_contra hCon
    obtain ‚ü®a, ha‚ü© : {x | f x ‚â† g x ‚àß f x = g x}.Nonempty := by
      have h‚ÇÅ := (eventually_nhdsWithin_of_eventually_nhds
        ((hf.ne_iff_eventually_ne hg).1 hCon)).and hfg
      have h‚ÇÇ : ‚àÖ ‚àâ ùìù[‚â†] x := by exact empty_notMem (ùìù[‚â†] x)
      simp_all
    simp at ha
  ¬∑ exact hfg.filter_mono nhdsWithin_le_nhds

@[deprecated (since := "2025-05-22")]
alias ContinuousAt.eventuallyEq_nhd_iff_eventuallyEq_nhdNE :=
  ContinuousAt.eventuallyEq_nhds_iff_eventuallyEq_nhdsNE

/-- A continuous map from a compact space to a Hausdorff space is a closed map. -/
protected theorem Continuous.isClosedMap [CompactSpace X] [T2Space Y] {f : X ‚Üí Y}
    (h : Continuous f) : IsClosedMap f := fun _s hs => (hs.isCompact.image h).isClosed

/-- A continuous injective map from a compact space to a Hausdorff space is a closed embedding. -/
theorem Continuous.isClosedEmbedding [CompactSpace X] [T2Space Y] {f : X ‚Üí Y} (h : Continuous f)
    (hf : Function.Injective f) : IsClosedEmbedding f :=
  .of_continuous_injective_isClosedMap h hf h.isClosedMap

/-- A continuous surjective map from a compact space to a Hausdorff space is a quotient map. -/
theorem IsQuotientMap.of_surjective_continuous [CompactSpace X] [T2Space Y] {f : X ‚Üí Y}
    (hsurj : Surjective f) (hcont : Continuous f) : IsQuotientMap f :=
  hcont.isClosedMap.isQuotientMap hcont hsurj

theorem isPreirreducible_iff_subsingleton [T2Space X] {S : Set X} :
    IsPreirreducible S ‚Üî S.Subsingleton := by
  refine ‚ü®fun h x hx y hy => ?_, Set.Subsingleton.isPreirreducible‚ü©
  by_contra e
  obtain ‚ü®U, V, hU, hV, hxU, hyV, h'‚ü© := t2_separation e
  exact ((h U V hU hV ‚ü®x, hx, hxU‚ü© ‚ü®y, hy, hyV‚ü©).mono inter_subset_right).not_disjoint h'

-- todo: use `alias` + `attribute [protected]` once we get `attribute [protected]`
protected lemma IsPreirreducible.subsingleton [T2Space X] {S : Set X} (h : IsPreirreducible S) :
    S.Subsingleton :=
  isPreirreducible_iff_subsingleton.1 h

theorem isIrreducible_iff_singleton [T2Space X] {S : Set X} : IsIrreducible S ‚Üî ‚àÉ x, S = {x} := by
  rw [IsIrreducible, isPreirreducible_iff_subsingleton,
    exists_eq_singleton_iff_nonempty_subsingleton]

/-- There does not exist a nontrivial preirreducible T‚ÇÇ space. -/
theorem not_preirreducible_nontrivial_t2 (X) [TopologicalSpace X] [PreirreducibleSpace X]
    [Nontrivial X] [T2Space X] : False :=
  (PreirreducibleSpace.isPreirreducible_univ (X := X)).subsingleton.not_nontrivial nontrivial_univ

theorem t2Space_antitone {X : Type*} : Antitone (@T2Space X) :=
  fun inst‚ÇÅ inst‚ÇÇ h_top h_t2 ‚Ü¶ @T2Space.of_injective_continuous _ _ inst‚ÇÅ inst‚ÇÇ
    h_t2 _ Function.injective_id <| continuous_id_of_le h_top

end Separation
