/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro
-/
import Mathlib.Topology.Compactness.SigmaCompact
import Mathlib.Topology.Irreducible
import Mathlib.Topology.Separation.Basic

/-!
# Tâ‚‚ and Tâ‚‚.â‚… spaces.

This file defines the Tâ‚‚ (Hausdorff) condition, which is the most commonly-used among the various
separation axioms, and the related Tâ‚‚.â‚… condition.

## Main definitions

* `T2Space`: A Tâ‚‚/Hausdorff space is a space where, for every two points `x â‰  y`,
  there is two disjoint open sets, one containing `x`, and the other `y`. Tâ‚‚ implies Tâ‚ and Râ‚.
* `T25Space`: A Tâ‚‚.â‚…/Urysohn space is a space where, for every two points `x â‰  y`,
  there is two open sets, one containing `x`, and the other `y`, whose closures are disjoint.
  Tâ‚‚.â‚… implies Tâ‚‚.

See `Mathlib/Topology/Separation/Regular.lean` for regular, Tâ‚ƒ, etc spaces; and
`Mathlib/Topology/Separation/GDelta.lean` for the definitions of `PerfectlyNormalSpace` and
`T6Space`.

Note that `mathlib` adopts the modern convention that `m â‰¤ n` if and only if `T_m â†’ T_n`, but
occasionally the literature swaps definitions for e.g. Tâ‚ƒ and regular.

## Main results

### Tâ‚‚ spaces

* `t2_iff_nhds`: A space is Tâ‚‚ iff the neighbourhoods of distinct points generate the bottom filter.
* `t2_iff_isClosed_diagonal`: A space is Tâ‚‚ iff the `diagonal` of `X` (that is, the set of all
  points of the form `(a, a) : X Ã— X`) is closed under the product topology.
* `separatedNhds_of_finset_finset`: Any two disjoint finsets are `SeparatedNhds`.
* Most topological constructions preserve Hausdorffness;
  these results are part of the typeclass inference system (e.g. `Topology.IsEmbedding.t2Space`)
* `Set.EqOn.closure`: If two functions are equal on some set `s`, they are equal on its closure.
* `IsCompact.isClosed`: All compact sets are closed.
* `WeaklyLocallyCompactSpace.locallyCompactSpace`: If a topological space is both
  weakly locally compact (i.e., each point has a compact neighbourhood)
  and is Tâ‚‚, then it is locally compact.
* `totallySeparatedSpace_of_t1_of_basis_clopen`: If `X` has a clopen basis, then
  it is a `TotallySeparatedSpace`.
* `loc_compact_t2_tot_disc_iff_tot_sep`: A locally compact Tâ‚‚ space is totally disconnected iff
  it is totally separated.
* `T2Quotient`: the largest T2 quotient of a given topological space.

If the space is also compact:

* `normalOfCompactT2`: A compact Tâ‚‚ space is a `NormalSpace`.
* `connectedComponent_eq_iInter_isClopen`: The connected component of a point
  is the intersection of all its clopen neighbourhoods.
* `compact_t2_tot_disc_iff_tot_sep`: Being a `TotallyDisconnectedSpace`
  is equivalent to being a `TotallySeparatedSpace`.
* `ConnectedComponents.t2`: `ConnectedComponents X` is Tâ‚‚ for `X` Tâ‚‚ and compact.

## References

* <https://en.wikipedia.org/wiki/Separation_axiom>
* [Willard's *General Topology*][zbMATH02107988]

-/

open Function Set Filter Topology TopologicalSpace

universe u v

variable {X : Type*} {Y : Type*} [TopologicalSpace X]

section Separation

/-- A Tâ‚‚ space, also known as a Hausdorff space, is one in which for every
  `x â‰  y` there exists disjoint open sets around `x` and `y`. This is
  the most widely used of the separation axioms. -/
@[mk_iff]
class T2Space (X : Type u) [TopologicalSpace X] : Prop where
  /-- Every two points in a Hausdorff space admit disjoint open neighbourhoods. -/
  t2 : Pairwise fun x y â†¦ âˆƒ u v : Set X, IsOpen u âˆ§ IsOpen v âˆ§ x âˆˆ u âˆ§ y âˆˆ v âˆ§ Disjoint u v

/-- Two different points can be separated by open sets. -/
theorem t2_separation [T2Space X] {x y : X} (h : x â‰  y) :
    âˆƒ u v : Set X, IsOpen u âˆ§ IsOpen v âˆ§ x âˆˆ u âˆ§ y âˆˆ v âˆ§ Disjoint u v :=
  T2Space.t2 h

-- todo: use this as a definition?
theorem t2Space_iff_disjoint_nhds : T2Space X â†” Pairwise fun x y : X â†¦ Disjoint (ğ“ x) (ğ“ y) := by
  refine (t2Space_iff X).trans (forallâ‚ƒ_congr fun x y _ â†¦ ?_)
  simp only [(nhds_basis_opens x).disjoint_iff (nhds_basis_opens y), â† exists_and_left,
    and_assoc, and_comm, and_left_comm]

@[simp]
theorem disjoint_nhds_nhds [T2Space X] {x y : X} : Disjoint (ğ“ x) (ğ“ y) â†” x â‰  y :=
  âŸ¨fun hd he â†¦ by simp [he, nhds_neBot.ne] at hd, (t2Space_iff_disjoint_nhds.mp â€¹_â€º Â·)âŸ©

theorem pairwise_disjoint_nhds [T2Space X] : Pairwise (Disjoint on (ğ“ : X â†’ Filter X)) := fun _ _ â†¦
  disjoint_nhds_nhds.2

protected theorem Set.pairwiseDisjoint_nhds [T2Space X] (s : Set X) : s.PairwiseDisjoint ğ“ :=
  pairwise_disjoint_nhds.set_pairwise s

/-- Points of a finite set can be separated by open sets from each other. -/
theorem Set.Finite.t2_separation [T2Space X] {s : Set X} (hs : s.Finite) :
    âˆƒ U : X â†’ Set X, (âˆ€ x, x âˆˆ U x âˆ§ IsOpen (U x)) âˆ§ s.PairwiseDisjoint U :=
  s.pairwiseDisjoint_nhds.exists_mem_filter_basis hs nhds_basis_opens

-- see Note [lower instance priority]
instance (priority := 100) T2Space.t1Space [T2Space X] : T1Space X :=
  t1Space_iff_disjoint_pure_nhds.mpr fun _ _ hne â†¦
    (disjoint_nhds_nhds.2 hne).mono_left <| pure_le_nhds _

-- see Note [lower instance priority]
instance (priority := 100) T2Space.r1Space [T2Space X] : R1Space X :=
  âŸ¨fun x y â†¦ (eq_or_ne x y).imp specializes_of_eq disjoint_nhds_nhds.2âŸ©

theorem SeparationQuotient.t2Space_iff : T2Space (SeparationQuotient X) â†” R1Space X := by
  simp only [t2Space_iff_disjoint_nhds, Pairwise, surjective_mk.forallâ‚‚, ne_eq, mk_eq_mk,
    r1Space_iff_inseparable_or_disjoint_nhds, â† disjoint_comap_iff surjective_mk, comap_mk_nhds_mk,
    â† or_iff_not_imp_left]

instance SeparationQuotient.t2Space [R1Space X] : T2Space (SeparationQuotient X) :=
  t2Space_iff.2 â€¹_â€º

instance (priority := 80) [R1Space X] [T0Space X] : T2Space X :=
  t2Space_iff_disjoint_nhds.2 fun _x _y hne â†¦ disjoint_nhds_nhds_iff_not_inseparable.2 fun hxy â†¦
    hne hxy.eq

theorem R1Space.t2Space_iff_t0Space [R1Space X] : T2Space X â†” T0Space X := by
  constructor <;> intro <;> infer_instance

/-- A space is Tâ‚‚ iff the neighbourhoods of distinct points generate the bottom filter. -/
theorem t2_iff_nhds : T2Space X â†” âˆ€ {x y : X}, NeBot (ğ“ x âŠ“ ğ“ y) â†’ x = y := by
  simp only [t2Space_iff_disjoint_nhds, disjoint_iff, neBot_iff, Ne, not_imp_comm, Pairwise]

theorem eq_of_nhds_neBot [T2Space X] {x y : X} (h : NeBot (ğ“ x âŠ“ ğ“ y)) : x = y :=
  t2_iff_nhds.mp â€¹_â€º h

theorem t2Space_iff_nhds :
    T2Space X â†” Pairwise fun x y : X â†¦ âˆƒ U âˆˆ ğ“ x, âˆƒ V âˆˆ ğ“ y, Disjoint U V := by
  simp only [t2Space_iff_disjoint_nhds, Filter.disjoint_iff, Pairwise]

theorem t2_separation_nhds [T2Space X] {x y : X} (h : x â‰  y) :
    âˆƒ u v, u âˆˆ ğ“ x âˆ§ v âˆˆ ğ“ y âˆ§ Disjoint u v :=
  let âŸ¨u, v, open_u, open_v, x_in, y_in, huvâŸ© := t2_separation h
  âŸ¨u, v, open_u.mem_nhds x_in, open_v.mem_nhds y_in, huvâŸ©

theorem t2_separation_compact_nhds [LocallyCompactSpace X] [T2Space X] {x y : X} (h : x â‰  y) :
    âˆƒ u v, u âˆˆ ğ“ x âˆ§ v âˆˆ ğ“ y âˆ§ IsCompact u âˆ§ IsCompact v âˆ§ Disjoint u v := by
  simpa only [exists_prop, â† exists_and_left, and_comm, and_assoc, and_left_comm] using
    ((compact_basis_nhds x).disjoint_iff (compact_basis_nhds y)).1 (disjoint_nhds_nhds.2 h)

theorem t2_iff_ultrafilter :
    T2Space X â†” âˆ€ {x y : X} (f : Ultrafilter X), â†‘f â‰¤ ğ“ x â†’ â†‘f â‰¤ ğ“ y â†’ x = y :=
  t2_iff_nhds.trans <| by simp only [â† exists_ultrafilter_iff, and_imp, le_inf_iff, exists_imp]

theorem t2_iff_isClosed_diagonal : T2Space X â†” IsClosed (diagonal X) := by
  simp only [t2Space_iff_disjoint_nhds, â† isOpen_compl_iff, isOpen_iff_mem_nhds, Prod.forall,
    nhds_prod_eq, compl_diagonal_mem_prod, mem_compl_iff, mem_diagonal_iff, Pairwise]

theorem isClosed_diagonal [T2Space X] : IsClosed (diagonal X) :=
  t2_iff_isClosed_diagonal.mp â€¹_â€º

theorem tendsto_nhds_unique [T2Space X] {f : Y â†’ X} {l : Filter Y} {a b : X} [NeBot l]
    (ha : Tendsto f l (ğ“ a)) (hb : Tendsto f l (ğ“ b)) : a = b :=
  (tendsto_nhds_unique_inseparable ha hb).eq

theorem tendsto_nhds_unique' [T2Space X] {f : Y â†’ X} {l : Filter Y} {a b : X} (_ : NeBot l)
    (ha : Tendsto f l (ğ“ a)) (hb : Tendsto f l (ğ“ b)) : a = b :=
  tendsto_nhds_unique ha hb

theorem tendsto_nhds_unique_of_eventuallyEq [T2Space X] {f g : Y â†’ X} {l : Filter Y} {a b : X}
    [NeBot l] (ha : Tendsto f l (ğ“ a)) (hb : Tendsto g l (ğ“ b)) (hfg : f =á¶ [l] g) : a = b :=
  tendsto_nhds_unique (ha.congr' hfg) hb

theorem tendsto_nhds_unique_of_frequently_eq [T2Space X] {f g : Y â†’ X} {l : Filter Y} {a b : X}
    (ha : Tendsto f l (ğ“ a)) (hb : Tendsto g l (ğ“ b)) (hfg : âˆƒá¶  x in l, f x = g x) : a = b :=
  have : âˆƒá¶  z : X Ã— X in ğ“ (a, b), z.1 = z.2 := (ha.prodMk_nhds hb).frequently hfg
  not_not.1 fun hne â†¦ this (isClosed_diagonal.isOpen_compl.mem_nhds hne)

/-- If `s` and `t` are compact sets in a Tâ‚‚ space, then the set neighborhoods filter of `s âˆ© t`
is the infimum of set neighborhoods filters for `s` and `t`.

For general sets, only the `â‰¤` inequality holds, see `nhdsSet_inter_le`. -/
theorem IsCompact.nhdsSet_inter_eq [T2Space X] {s t : Set X} (hs : IsCompact s) (ht : IsCompact t) :
    ğ“Ë¢ (s âˆ© t) = ğ“Ë¢ s âŠ“ ğ“Ë¢ t := by
  refine le_antisymm (nhdsSet_inter_le _ _) ?_
  simp_rw [hs.nhdsSet_inf_eq_biSup, ht.inf_nhdsSet_eq_biSup, nhdsSet, sSup_image]
  refine iSupâ‚‚_le fun x hxs â†¦ iSupâ‚‚_le fun y hyt â†¦ ?_
  rcases eq_or_ne x y with (rfl | hne)
  Â· exact le_iSupâ‚‚_of_le x âŸ¨hxs, hytâŸ© (inf_idem _).le
  Â· exact (disjoint_nhds_nhds.mpr hne).eq_bot â–¸ bot_le

/-- In a `T2Space X`, for a compact set `t` and a point `x` outside `t`, there are open sets `U`,
`V` that separate `t` and `x`. -/
lemma IsCompact.separation_of_notMem {X : Type u_1} [TopologicalSpace X] [T2Space X] {x : X}
    {t : Set X} (H1 : IsCompact t) (H2 : x âˆ‰ t) :
    âˆƒ (U : Set X), âˆƒ (V : Set X), IsOpen U âˆ§ IsOpen V âˆ§ t âŠ† U âˆ§ x âˆˆ V âˆ§ Disjoint U V := by
  simpa [SeparatedNhds] using SeparatedNhds.of_isCompact_isCompact_isClosed H1 isCompact_singleton
    isClosed_singleton <| disjoint_singleton_right.mpr H2

@[deprecated (since := "2025-05-23")]
alias IsCompact.separation_of_not_mem := IsCompact.separation_of_notMem

/-- In a `T2Space X`, for a compact set `t` and a point `x` outside `t`, `ğ“Ë¢ t` and `ğ“ x` are
disjoint. -/
lemma IsCompact.disjoint_nhdsSet_nhds {X : Type u_1} [TopologicalSpace X] [T2Space X] {x : X}
    {t : Set X} (H1 : IsCompact t) (H2 : x âˆ‰ t) :
    Disjoint (ğ“Ë¢ t) (ğ“ x) := by
  simpa using SeparatedNhds.disjoint_nhdsSet <| .of_isCompact_isCompact_isClosed H1
    isCompact_singleton isClosed_singleton <| disjoint_singleton_right.mpr H2

/-- If a function `f` is

- injective on a compact set `s`;
- continuous at every point of this set;
- injective on a neighborhood of each point of this set,

then it is injective on a neighborhood of this set. -/
theorem Set.InjOn.exists_mem_nhdsSet {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    [T2Space Y] {f : X â†’ Y} {s : Set X} (inj : InjOn f s) (sc : IsCompact s)
    (fc : âˆ€ x âˆˆ s, ContinuousAt f x) (loc : âˆ€ x âˆˆ s, âˆƒ u âˆˆ ğ“ x, InjOn f u) :
    âˆƒ t âˆˆ ğ“Ë¢ s, InjOn f t := by
  have : âˆ€ x âˆˆ s Ã—Ë¢ s, âˆ€á¶  y in ğ“ x, f y.1 = f y.2 â†’ y.1 = y.2 := fun (x, y) âŸ¨hx, hyâŸ© â†¦ by
    rcases eq_or_ne x y with rfl | hne
    Â· rcases loc x hx with âŸ¨u, hu, hfâŸ©
      exact Filter.mem_of_superset (prod_mem_nhds hu hu) <| forall_prod_set.2 hf
    Â· suffices âˆ€á¶  z in ğ“ (x, y), f z.1 â‰  f z.2 from this.mono fun _ hne h â†¦ absurd h hne
      refine (fc x hx).prodMap' (fc y hy) <| isClosed_diagonal.isOpen_compl.mem_nhds ?_
      exact inj.ne hx hy hne
  rw [â† eventually_nhdsSet_iff_forall, sc.nhdsSet_prod_eq sc] at this
  exact eventually_prod_self_iff.1 this

/-- If a function `f` is

- injective on a compact set `s`;
- continuous at every point of this set;
- injective on a neighborhood of each point of this set,

then it is injective on an open neighborhood of this set. -/
theorem Set.InjOn.exists_isOpen_superset {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    [T2Space Y] {f : X â†’ Y} {s : Set X} (inj : InjOn f s) (sc : IsCompact s)
    (fc : âˆ€ x âˆˆ s, ContinuousAt f x) (loc : âˆ€ x âˆˆ s, âˆƒ u âˆˆ ğ“ x, InjOn f u) :
    âˆƒ t, IsOpen t âˆ§ s âŠ† t âˆ§ InjOn f t :=
  let âŸ¨_t, hst, htâŸ© := inj.exists_mem_nhdsSet sc fc loc
  let âŸ¨u, huo, hsu, hutâŸ© := mem_nhdsSet_iff_exists.1 hst
  âŸ¨u, huo, hsu, ht.mono hutâŸ©

section limUnder

variable [T2Space X] {f : Filter X}

/-!
### Properties of `lim` and `limUnder`

In this section we use explicit `Nonempty X` instances for `lim` and `limUnder`. This way the lemmas
are useful without a `Nonempty X` instance.
-/


theorem lim_eq {x : X} [NeBot f] (h : f â‰¤ ğ“ x) : @lim _ _ âŸ¨xâŸ© f = x :=
  tendsto_nhds_unique (le_nhds_lim âŸ¨x, hâŸ©) h

theorem lim_eq_iff [NeBot f] (h : âˆƒ x : X, f â‰¤ ğ“ x) {x} : @lim _ _ âŸ¨xâŸ© f = x â†” f â‰¤ ğ“ x :=
  âŸ¨fun c â†¦ c â–¸ le_nhds_lim h, lim_eqâŸ©

theorem Ultrafilter.lim_eq_iff_le_nhds [CompactSpace X] {x : X} {F : Ultrafilter X} :
    F.lim = x â†” â†‘F â‰¤ ğ“ x :=
  âŸ¨fun h â†¦ h â–¸ F.le_nhds_lim, lim_eqâŸ©

theorem isOpen_iff_ultrafilter' [CompactSpace X] (U : Set X) :
    IsOpen U â†” âˆ€ F : Ultrafilter X, F.lim âˆˆ U â†’ U âˆˆ F.1 := by
  rw [isOpen_iff_ultrafilter]
  refine âŸ¨fun h F hF â†¦ h F.lim hF F F.le_nhds_lim, ?_âŸ©
  intro cond x hx f h
  rw [â† Ultrafilter.lim_eq_iff_le_nhds.2 h] at hx
  exact cond _ hx

theorem Filter.Tendsto.limUnder_eq {x : X} {f : Filter Y} [NeBot f] {g : Y â†’ X}
    (h : Tendsto g f (ğ“ x)) : @limUnder _ _ _ âŸ¨xâŸ© f g = x :=
  lim_eq h

theorem Filter.limUnder_eq_iff {f : Filter Y} [NeBot f] {g : Y â†’ X} (h : âˆƒ x, Tendsto g f (ğ“ x))
    {x} : @limUnder _ _ _ âŸ¨xâŸ© f g = x â†” Tendsto g f (ğ“ x) :=
  âŸ¨fun c â†¦ c â–¸ tendsto_nhds_limUnder h, Filter.Tendsto.limUnder_eqâŸ©

theorem Continuous.limUnder_eq [TopologicalSpace Y] {f : Y â†’ X} (h : Continuous f) (y : Y) :
    @limUnder _ _ _ âŸ¨f yâŸ© (ğ“ y) f = f y :=
  (h.tendsto y).limUnder_eq

@[simp]
theorem lim_nhds (x : X) : @lim _ _ âŸ¨xâŸ© (ğ“ x) = x :=
  lim_eq le_rfl

@[simp]
theorem limUnder_nhds_id (x : X) : @limUnder _ _ _ âŸ¨xâŸ© (ğ“ x) id = x :=
  lim_nhds x

@[simp]
theorem lim_nhdsWithin {x : X} {s : Set X} (h : x âˆˆ closure s) : @lim _ _ âŸ¨xâŸ© (ğ“[s] x) = x :=
  haveI : NeBot (ğ“[s] x) := mem_closure_iff_clusterPt.1 h
  lim_eq inf_le_left

@[simp]
theorem limUnder_nhdsWithin_id {x : X} {s : Set X} (h : x âˆˆ closure s) :
    @limUnder _ _ _ âŸ¨xâŸ© (ğ“[s] x) id = x :=
  lim_nhdsWithin h

end limUnder

/-!
### `T2Space` constructions

We use two lemmas to prove that various standard constructions generate Hausdorff spaces from
Hausdorff spaces:

* `separated_by_continuous` says that two points `x y : X` can be separated by open neighborhoods
  provided that there exists a continuous map `f : X â†’ Y` with a Hausdorff codomain such that
  `f x â‰  f y`. We use this lemma to prove that topological spaces defined using `induced` are
  Hausdorff spaces.

* `separated_by_isOpenEmbedding` says that for an open embedding `f : X â†’ Y` of a Hausdorff space
  `X`, the images of two distinct points `x y : X`, `x â‰  y` can be separated by open neighborhoods.
  We use this lemma to prove that topological spaces defined using `coinduced` are Hausdorff spaces.
-/

-- see Note [lower instance priority]
instance (priority := 100) DiscreteTopology.toT2Space
    [DiscreteTopology X] : T2Space X :=
  âŸ¨fun x y h â†¦ âŸ¨{x}, {y}, isOpen_discrete _, isOpen_discrete _, rfl, rfl, disjoint_singleton.2 hâŸ©âŸ©

theorem separated_by_continuous [TopologicalSpace Y] [T2Space Y]
    {f : X â†’ Y} (hf : Continuous f) {x y : X} (h : f x â‰  f y) :
    âˆƒ u v : Set X, IsOpen u âˆ§ IsOpen v âˆ§ x âˆˆ u âˆ§ y âˆˆ v âˆ§ Disjoint u v :=
  let âŸ¨u, v, uo, vo, xu, yv, uvâŸ© := t2_separation h
  âŸ¨f â»Â¹' u, f â»Â¹' v, uo.preimage hf, vo.preimage hf, xu, yv, uv.preimage _âŸ©

theorem separated_by_isOpenEmbedding [TopologicalSpace Y] [T2Space X]
    {f : X â†’ Y} (hf : IsOpenEmbedding f) {x y : X} (h : x â‰  y) :
    âˆƒ u v : Set Y, IsOpen u âˆ§ IsOpen v âˆ§ f x âˆˆ u âˆ§ f y âˆˆ v âˆ§ Disjoint u v :=
  let âŸ¨u, v, uo, vo, xu, yv, uvâŸ© := t2_separation h
  âŸ¨f '' u, f '' v, hf.isOpenMap _ uo, hf.isOpenMap _ vo, mem_image_of_mem _ xu,
    mem_image_of_mem _ yv, disjoint_image_of_injective hf.injective uvâŸ©

instance {p : X â†’ Prop} [T2Space X] : T2Space (Subtype p) := inferInstance

instance Prod.t2Space [T2Space X] [TopologicalSpace Y] [T2Space Y] : T2Space (X Ã— Y) :=
  inferInstance

/-- If the codomain of an injective continuous function is a Hausdorff space, then so is its
domain. -/
theorem T2Space.of_injective_continuous [TopologicalSpace Y] [T2Space Y] {f : X â†’ Y}
    (hinj : Injective f) (hc : Continuous f) : T2Space X :=
  âŸ¨fun _ _ h â†¦ separated_by_continuous hc (hinj.ne h)âŸ©

/-- If the codomain of a topological embedding is a Hausdorff space, then so is its domain.
See also `T2Space.of_continuous_injective`. -/
theorem Topology.IsEmbedding.t2Space [TopologicalSpace Y] [T2Space Y] {f : X â†’ Y}
    (hf : IsEmbedding f) : T2Space X :=
  .of_injective_continuous hf.injective hf.continuous

protected theorem Homeomorph.t2Space [TopologicalSpace Y] [T2Space X] (h : X â‰ƒâ‚œ Y) : T2Space Y :=
  h.symm.isEmbedding.t2Space

instance ULift.instT2Space [T2Space X] : T2Space (ULift X) :=
  IsEmbedding.uliftDown.t2Space

instance [T2Space X] [TopologicalSpace Y] [T2Space Y] :
    T2Space (X âŠ• Y) := by
  constructor
  rintro (x | x) (y | y) h
  Â· exact separated_by_isOpenEmbedding .inl <| ne_of_apply_ne _ h
  Â· exact separated_by_continuous continuous_isLeft <| by simp
  Â· exact separated_by_continuous continuous_isLeft <| by simp
  Â· exact separated_by_isOpenEmbedding .inr <| ne_of_apply_ne _ h

instance Pi.t2Space {Y : X â†’ Type v} [âˆ€ a, TopologicalSpace (Y a)]
    [âˆ€ a, T2Space (Y a)] : T2Space (âˆ€ a, Y a) :=
  inferInstance

instance Sigma.t2Space {Î¹} {X : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (X i)] [âˆ€ a, T2Space (X a)] :
    T2Space (Î£ i, X i) := by
  constructor
  rintro âŸ¨i, xâŸ© âŸ¨j, yâŸ© neq
  rcases eq_or_ne i j with (rfl | h)
  Â· replace neq : x â‰  y := ne_of_apply_ne _ neq
    exact separated_by_isOpenEmbedding .sigmaMk neq
  Â· let _ := (âŠ¥ : TopologicalSpace Î¹); have : DiscreteTopology Î¹ := âŸ¨rflâŸ©
    exact separated_by_continuous (continuous_def.2 fun u _ â†¦ isOpen_sigma_fst_preimage u) h

section
variable (X)

/-- The smallest equivalence relation on a topological space giving a T2 quotient. -/
def t2Setoid : Setoid X := sInf {s | T2Space (Quotient s)}

/-- The largest T2 quotient of a topological space. This construction is left-adjoint to the
inclusion of T2 spaces into all topological spaces. -/
def T2Quotient := Quotient (t2Setoid X)

@[deprecated (since := "2025-05-15")] alias t2Quotient := T2Quotient

namespace T2Quotient
variable {X}

instance : TopologicalSpace (T2Quotient X) :=
  inferInstanceAs <| TopologicalSpace (Quotient _)

/-- The map from a topological space to its largest T2 quotient. -/
def mk : X â†’ T2Quotient X := Quotient.mk (t2Setoid X)

lemma mk_eq {x y : X} : mk x = mk y â†” âˆ€ s : Setoid X, T2Space (Quotient s) â†’ s x y :=
  Setoid.quotient_mk_sInf_eq

variable (X)

lemma surjective_mk : Surjective (mk : X â†’ T2Quotient X) := Quotient.mk_surjective

lemma continuous_mk : Continuous (mk : X â†’ T2Quotient X) :=
  continuous_quotient_mk'

variable {X}

@[elab_as_elim]
protected lemma inductionOn {motive : T2Quotient X â†’ Prop} (q : T2Quotient X)
    (h : âˆ€ x, motive (T2Quotient.mk x)) : motive q := Quotient.inductionOn q h

@[elab_as_elim]
protected lemma inductionOnâ‚‚ [TopologicalSpace Y] {motive : T2Quotient X â†’ T2Quotient Y â†’ Prop}
    (q : T2Quotient X) (q' : T2Quotient Y) (h : âˆ€ x y, motive (mk x) (mk y)) : motive q q' :=
  Quotient.inductionOnâ‚‚ q q' h

/-- The largest T2 quotient of a topological space is indeed T2. -/
instance : T2Space (T2Quotient X) := by
  rw [t2Space_iff]
  rintro âŸ¨xâŸ© âŸ¨yâŸ© (h : Â¬ T2Quotient.mk x = T2Quotient.mk y)
  obtain âŸ¨s, hs, hsxyâŸ© : âˆƒ s, T2Space (Quotient s) âˆ§ Quotient.mk s x â‰  Quotient.mk s y := by
    simpa [T2Quotient.mk_eq] using h
  exact separated_by_continuous (continuous_map_sInf (by exact hs)) hsxy

lemma compatible {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] [T2Space Y]
    {f : X â†’ Y} (hf : Continuous f) : letI _ := t2Setoid X
    âˆ€ (a b : X), a â‰ˆ b â†’ f a = f b := by
  change t2Setoid X â‰¤ Setoid.ker f
  exact sInf_le <| .of_injective_continuous
    (Setoid.ker_lift_injective _) (hf.quotient_lift fun _ _ â†¦ id)

/-- The universal property of the largest T2 quotient of a topological space `X`: any continuous
map from `X` to a T2 space `Y` uniquely factors through `T2Quotient X`. This declaration builds the
factored map. Its continuity is `T2Quotient.continuous_lift`, the fact that it indeed factors the
original map is `T2Quotient.lift_mk` and uniqueness is `T2Quotient.unique_lift`. -/
def lift {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] [T2Space Y]
    {f : X â†’ Y} (hf : Continuous f) : T2Quotient X â†’ Y :=
  Quotient.lift f (T2Quotient.compatible hf)

lemma continuous_lift {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] [T2Space Y]
    {f : X â†’ Y} (hf : Continuous f) : Continuous (T2Quotient.lift hf) :=
  continuous_coinduced_dom.mpr hf

@[simp]
lemma lift_mk {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] [T2Space Y]
    {f : X â†’ Y} (hf : Continuous f) (x : X) : lift hf (mk x) = f x :=
  Quotient.lift_mk (s := t2Setoid X) f (T2Quotient.compatible hf) x

lemma unique_lift {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y] [T2Space Y]
    {f : X â†’ Y} (hf : Continuous f) {g : T2Quotient X â†’ Y} (hfg : g âˆ˜ mk = f) :
    g = lift hf := by
  apply surjective_mk X |>.right_cancellable |>.mp <| funext _
  simp [â† hfg]

end T2Quotient
end

variable {Z : Type*} [TopologicalSpace Y] [TopologicalSpace Z]

theorem isClosed_eq [T2Space X] {f g : Y â†’ X} (hf : Continuous f) (hg : Continuous g) :
    IsClosed { y : Y | f y = g y } :=
  continuous_iff_isClosed.mp (hf.prodMk hg) _ isClosed_diagonal

/-- If functions `f` and `g` are continuous on a closed set `s`,
then the set of points `x âˆˆ s` such that `f x = g x` is a closed set. -/
protected theorem IsClosed.isClosed_eq [T2Space Y] {f g : X â†’ Y} {s : Set X} (hs : IsClosed s)
    (hf : ContinuousOn f s) (hg : ContinuousOn g s) : IsClosed {x âˆˆ s | f x = g x} :=
  (hf.prodMk hg).preimage_isClosed_of_isClosed hs isClosed_diagonal

theorem isOpen_ne_fun [T2Space X] {f g : Y â†’ X} (hf : Continuous f) (hg : Continuous g) :
    IsOpen { y : Y | f y â‰  g y } :=
  isOpen_compl_iff.mpr <| isClosed_eq hf hg

/-- If two continuous maps are equal on `s`, then they are equal on the closure of `s`. See also
`Set.EqOn.of_subset_closure` for a more general version. -/
protected theorem Set.EqOn.closure [T2Space X] {s : Set Y} {f g : Y â†’ X} (h : EqOn f g s)
    (hf : Continuous f) (hg : Continuous g) : EqOn f g (closure s) :=
  closure_minimal h (isClosed_eq hf hg)

/-- If two continuous functions are equal on a dense set, then they are equal. -/
theorem Continuous.ext_on [T2Space X] {s : Set Y} (hs : Dense s) {f g : Y â†’ X} (hf : Continuous f)
    (hg : Continuous g) (h : EqOn f g s) : f = g :=
  funext fun x â†¦ h.closure hf hg (hs x)

theorem eqOn_closureâ‚‚' [T2Space Z] {s : Set X} {t : Set Y} {f g : X â†’ Y â†’ Z}
    (h : âˆ€ x âˆˆ s, âˆ€ y âˆˆ t, f x y = g x y) (hfâ‚ : âˆ€ x, Continuous (f x))
    (hfâ‚‚ : âˆ€ y, Continuous fun x â†¦ f x y) (hgâ‚ : âˆ€ x, Continuous (g x))
    (hgâ‚‚ : âˆ€ y, Continuous fun x â†¦ g x y) : âˆ€ x âˆˆ closure s, âˆ€ y âˆˆ closure t, f x y = g x y :=
  suffices closure s âŠ† â‹‚ y âˆˆ closure t, { x | f x y = g x y } by simpa only [subset_def, mem_iInter]
  (closure_minimal fun x hx â†¦ mem_iInterâ‚‚.2 <| Set.EqOn.closure (h x hx) (hfâ‚ _) (hgâ‚ _)) <|
    isClosed_biInter fun _ _ â†¦ isClosed_eq (hfâ‚‚ _) (hgâ‚‚ _)

theorem eqOn_closureâ‚‚ [T2Space Z] {s : Set X} {t : Set Y} {f g : X â†’ Y â†’ Z}
    (h : âˆ€ x âˆˆ s, âˆ€ y âˆˆ t, f x y = g x y) (hf : Continuous (uncurry f))
    (hg : Continuous (uncurry g)) : âˆ€ x âˆˆ closure s, âˆ€ y âˆˆ closure t, f x y = g x y :=
  eqOn_closureâ‚‚' h hf.uncurry_left hf.uncurry_right hg.uncurry_left hg.uncurry_right

/-- If `f x = g x` for all `x âˆˆ s` and `f`, `g` are continuous on `t`, `s âŠ† t âŠ† closure s`, then
`f x = g x` for all `x âˆˆ t`. See also `Set.EqOn.closure`. -/
theorem Set.EqOn.of_subset_closure [T2Space Y] {s t : Set X} {f g : X â†’ Y} (h : EqOn f g s)
    (hf : ContinuousOn f t) (hg : ContinuousOn g t) (hst : s âŠ† t) (hts : t âŠ† closure s) :
    EqOn f g t := by
  intro x hx
  have : (ğ“[s] x).NeBot := mem_closure_iff_clusterPt.mp (hts hx)
  exact
    tendsto_nhds_unique_of_eventuallyEq ((hf x hx).mono_left <| nhdsWithin_mono _ hst)
      ((hg x hx).mono_left <| nhdsWithin_mono _ hst) (h.eventuallyEq_of_mem self_mem_nhdsWithin)

theorem Function.LeftInverse.isClosed_range [T2Space X] {f : X â†’ Y} {g : Y â†’ X}
    (h : Function.LeftInverse f g) (hf : Continuous f) (hg : Continuous g) : IsClosed (range g) :=
  have : EqOn (g âˆ˜ f) id (closure <| range g) :=
    h.rightInvOn_range.eqOn.closure (hg.comp hf) continuous_id
  isClosed_of_closure_subset fun x hx â†¦ âŸ¨f x, this hxâŸ©

theorem Function.LeftInverse.isClosedEmbedding [T2Space X] {f : X â†’ Y} {g : Y â†’ X}
    (h : Function.LeftInverse f g) (hf : Continuous f) (hg : Continuous g) : IsClosedEmbedding g :=
  âŸ¨.of_leftInverse h hf hg, h.isClosed_range hf hgâŸ©

theorem SeparatedNhds.of_isCompact_isCompact [T2Space X] {s t : Set X} (hs : IsCompact s)
    (ht : IsCompact t) (hst : Disjoint s t) : SeparatedNhds s t := by
  simp only [SeparatedNhds, prod_subset_compl_diagonal_iff_disjoint.symm] at hst âŠ¢
  exact generalized_tube_lemma hs ht isClosed_diagonal.isOpen_compl hst

/-- In a `T2Space X`, for disjoint closed sets `s t` such that `closure sá¶œ` is compact,
there are neighbourhoods that separate `s` and `t`. -/
lemma SeparatedNhds.of_isClosed_isCompact_closure_compl_isClosed [T2Space X] {s : Set X}
    {t : Set X} (H1 : IsClosed s) (H2 : IsCompact (closure sá¶œ)) (H3 : IsClosed t)
    (H4 : Disjoint s t) : SeparatedNhds s t := by
  -- Since `t` is a closed subset of the compact set `closure sá¶œ`, it is compact.
  have ht : IsCompact t := .of_isClosed_subset H2 H3 <| H4.subset_compl_left.trans subset_closure
  -- we split `s` into its frontier and its interior.
  rw [â† diff_union_of_subset (interior_subset (s := s))]
  -- since `t âŠ† sá¶œ`, which is open, and `interior s` is open, we have
  -- `SeparatedNhds (interior s) t`, which leaves us only with the frontier.
  refine .union_left ?_ âŸ¨interior s, sá¶œ, isOpen_interior, H1.isOpen_compl, le_rfl,
    H4.subset_compl_left, disjoint_compl_right.mono_left interior_subsetâŸ©
  -- Since the frontier of `s` is compact (as it is a subset of `closure sá¶œ`), we simply apply
  -- `SeparatedNhds_of_isCompact_isCompact`.
  rw [â† H1.frontier_eq, frontier_eq_closure_inter_closure, H1.closure_eq]
  refine .of_isCompact_isCompact ?_ ht (disjoint_of_subset_left inter_subset_left H4)
  exact H2.of_isClosed_subset (H1.inter isClosed_closure) inter_subset_right

section SeparatedFinset

theorem SeparatedNhds.of_finset_finset [T2Space X] (s t : Finset X) (h : Disjoint s t) :
    SeparatedNhds (s : Set X) t :=
  .of_isCompact_isCompact s.finite_toSet.isCompact t.finite_toSet.isCompact <| mod_cast h

theorem SeparatedNhds.of_singleton_finset [T2Space X] {x : X} {s : Finset X} (h : x âˆ‰ s) :
    SeparatedNhds ({x} : Set X) s :=
  mod_cast .of_finset_finset {x} s (Finset.disjoint_singleton_left.mpr h)

end SeparatedFinset

/-- In a `T2Space`, every compact set is closed. -/
theorem IsCompact.isClosed [T2Space X] {s : Set X} (hs : IsCompact s) : IsClosed s :=
  isClosed_iff_forall_filter.2 fun _x _f _ hfs hfx â†¦
    let âŸ¨_y, hy, hfyâŸ© := hs.exists_clusterPt hfs
    mem_of_eq_of_mem (eq_of_nhds_neBot (hfy.mono hfx).neBot).symm hy

theorem IsCompact.preimage_continuous [CompactSpace X] [T2Space Y] {f : X â†’ Y} {s : Set Y}
    (hs : IsCompact s) (hf : Continuous f) : IsCompact (f â»Â¹' s) :=
  (hs.isClosed.preimage hf).isCompact

lemma Pi.isCompact_iff {Î¹ : Type*} {X : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (X i)]
    [âˆ€ i, T2Space (X i)] {s : Set (Î  i, X i)} :
    IsCompact s â†” IsClosed s âˆ§ âˆ€ i, IsCompact (eval i '' s) := by
  constructor <;> intro H
  Â· exact âŸ¨H.isClosed, fun i â†¦ H.image <| continuous_apply iâŸ©
  Â· exact IsCompact.of_isClosed_subset (isCompact_univ_pi H.2) H.1 (subset_pi_eval_image univ s)

lemma Pi.isCompact_closure_iff {Î¹ : Type*} {X : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (X i)]
    [âˆ€ i, T2Space (X i)] {s : Set (Î  i, X i)} :
    IsCompact (closure s) â†” âˆ€ i, IsCompact (closure <| eval i '' s) := by
  simp_rw [â† exists_isCompact_superset_iff, Pi.exists_compact_superset_iff, image_subset_iff]

/-- If `V : Î¹ â†’ Set X` is a decreasing family of compact sets then any neighborhood of
`â‹‚ i, V i` contains some `V i`. This is a version of `exists_subset_nhds_of_isCompact'` where we
don't need to assume each `V i` closed because it follows from compactness since `X` is
assumed to be Hausdorff. -/
theorem exists_subset_nhds_of_isCompact [T2Space X] {Î¹ : Type*} [Nonempty Î¹] {V : Î¹ â†’ Set X}
    (hV : Directed (Â· âŠ‡ Â·) V) (hV_cpct : âˆ€ i, IsCompact (V i)) {U : Set X}
    (hU : âˆ€ x âˆˆ â‹‚ i, V i, U âˆˆ ğ“ x) : âˆƒ i, V i âŠ† U :=
  exists_subset_nhds_of_isCompact' hV hV_cpct (fun i â†¦ (hV_cpct i).isClosed) hU

theorem CompactExhaustion.isClosed [T2Space X] (K : CompactExhaustion X) (n : â„•) : IsClosed (K n) :=
  (K.isCompact n).isClosed

theorem IsCompact.inter [T2Space X] {s t : Set X} (hs : IsCompact s) (ht : IsCompact t) :
    IsCompact (s âˆ© t) :=
  hs.inter_right <| ht.isClosed

theorem image_closure_of_isCompact [T2Space Y] {s : Set X} (hs : IsCompact (closure s)) {f : X â†’ Y}
    (hf : ContinuousOn f (closure s)) : f '' closure s = closure (f '' s) :=
  Subset.antisymm hf.image_closure <|
    closure_minimal (image_mono subset_closure) (hs.image_of_continuousOn hf).isClosed

/-- Two continuous maps into a Hausdorff space agree at a point iff they agree in a
neighborhood. -/
theorem ContinuousAt.ne_iff_eventually_ne [T2Space Y] {x : X} {f g : X â†’ Y}
    (hf : ContinuousAt f x) (hg : ContinuousAt g x) :
    f x â‰  g x â†” âˆ€á¶  x in ğ“ x, f x â‰  g x := by
  constructor <;> intro hfg
  Â· obtain âŸ¨Uf, Ug, hâ‚U, hâ‚‚U, hâ‚ƒU, hâ‚„U, hâ‚…UâŸ© := t2_separation hfg
    rw [Set.disjoint_iff_inter_eq_empty] at hâ‚…U
    filter_upwards [inter_mem
      (hf.preimage_mem_nhds (IsOpen.mem_nhds hâ‚U hâ‚ƒU))
      (hg.preimage_mem_nhds (IsOpen.mem_nhds hâ‚‚U hâ‚„U))]
    intro x hx
    simp only [Set.mem_inter_iff, Set.mem_preimage] at hx
    by_contra H
    rw [H] at hx
    have : g x âˆˆ Uf âˆ© Ug := hx
    simp [hâ‚…U] at this
  Â· obtain âŸ¨t, hâ‚t, hâ‚‚t, hâ‚ƒtâŸ© := eventually_nhds_iff.1 hfg
    exact hâ‚t x hâ‚ƒt

/-- **Local identity principle** for continuous maps: Two continuous maps into a Hausdorff space
agree in a punctured neighborhood of a non-isolated point iff they agree in a neighborhood. -/
theorem ContinuousAt.eventuallyEq_nhds_iff_eventuallyEq_nhdsNE [T2Space Y] {x : X} {f g : X â†’ Y}
    (hf : ContinuousAt f x) (hg : ContinuousAt g x) [(ğ“[â‰ ] x).NeBot] :
    f =á¶ [ğ“[â‰ ] x] g â†” f =á¶ [ğ“ x] g := by
  constructor <;> intro hfg
  Â· apply eventuallyEq_nhds_of_eventuallyEq_nhdsNE hfg
    by_contra hCon
    obtain âŸ¨a, haâŸ© : {x | f x â‰  g x âˆ§ f x = g x}.Nonempty := by
      have hâ‚ := (eventually_nhdsWithin_of_eventually_nhds
        ((hf.ne_iff_eventually_ne hg).1 hCon)).and hfg
      have hâ‚‚ : âˆ… âˆ‰ ğ“[â‰ ] x := by exact empty_notMem (ğ“[â‰ ] x)
      simp_all
    simp at ha
  Â· exact hfg.filter_mono nhdsWithin_le_nhds

@[deprecated (since := "2025-05-22")]
alias ContinuousAt.eventuallyEq_nhd_iff_eventuallyEq_nhdNE :=
  ContinuousAt.eventuallyEq_nhds_iff_eventuallyEq_nhdsNE

/-- A continuous map from a compact space to a Hausdorff space is a closed map. -/
protected theorem Continuous.isClosedMap [CompactSpace X] [T2Space Y] {f : X â†’ Y}
    (h : Continuous f) : IsClosedMap f := fun _s hs â†¦ (hs.isCompact.image h).isClosed

/-- A continuous injective map from a compact space to a Hausdorff space is a closed embedding. -/
theorem Continuous.isClosedEmbedding [CompactSpace X] [T2Space Y] {f : X â†’ Y} (h : Continuous f)
    (hf : Function.Injective f) : IsClosedEmbedding f :=
  .of_continuous_injective_isClosedMap h hf h.isClosedMap

/-- A continuous surjective map from a compact space to a Hausdorff space is a quotient map. -/
theorem IsQuotientMap.of_surjective_continuous [CompactSpace X] [T2Space Y] {f : X â†’ Y}
    (hsurj : Surjective f) (hcont : Continuous f) : IsQuotientMap f :=
  hcont.isClosedMap.isQuotientMap hcont hsurj

theorem isPreirreducible_iff_subsingleton [T2Space X] {S : Set X} :
    IsPreirreducible S â†” S.Subsingleton := by
  refine âŸ¨fun h x hx y hy â†¦ ?_, Set.Subsingleton.isPreirreducibleâŸ©
  by_contra e
  obtain âŸ¨U, V, hU, hV, hxU, hyV, h'âŸ© := t2_separation e
  exact ((h U V hU hV âŸ¨x, hx, hxUâŸ© âŸ¨y, hy, hyVâŸ©).mono inter_subset_right).not_disjoint h'

-- todo: use `alias` + `attribute [protected]` once we get `attribute [protected]`
protected lemma IsPreirreducible.subsingleton [T2Space X] {S : Set X} (h : IsPreirreducible S) :
    S.Subsingleton :=
  isPreirreducible_iff_subsingleton.1 h

theorem isIrreducible_iff_singleton [T2Space X] {S : Set X} : IsIrreducible S â†” âˆƒ x, S = {x} := by
  rw [IsIrreducible, isPreirreducible_iff_subsingleton,
    exists_eq_singleton_iff_nonempty_subsingleton]

/-- There does not exist a nontrivial preirreducible Tâ‚‚ space. -/
theorem not_preirreducible_nontrivial_t2 (X) [TopologicalSpace X] [PreirreducibleSpace X]
    [Nontrivial X] [T2Space X] : False :=
  (PreirreducibleSpace.isPreirreducible_univ (X := X)).subsingleton.not_nontrivial nontrivial_univ

theorem t2Space_antitone {X : Type*} : Antitone (@T2Space X) :=
  fun instâ‚ instâ‚‚ h_top h_t2 â†¦ @T2Space.of_injective_continuous _ _ instâ‚ instâ‚‚
    h_t2 _ Function.injective_id <| continuous_id_of_le h_top

end Separation
