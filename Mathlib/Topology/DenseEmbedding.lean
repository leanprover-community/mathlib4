/-
Copyright (c) 2019 Reid Barton. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Patrick Massot
-/
import Mathlib.Topology.Bases
import Mathlib.Topology.Separation.Regular

/-!
# Dense embeddings

This file defines three properties of functions:

* `DenseRange f`       means `f` has dense image;
* `IsDenseInducing i`  means `i` is also inducing, namely it induces the topology on its codomain;
* `IsDenseEmbedding e` means `e` is further an embedding, namely it is injective and `Inducing`.

The main theorem `continuous_extend` gives a criterion for a function
`f : X â†’ Z` to a Tâ‚ƒ space Z to extend along a dense embedding
`i : X â†’ Y` to a continuous function `g : Y â†’ Z`. Actually `i` only
has to be `IsDenseInducing` (not necessarily injective).

-/


noncomputable section

open Filter Set Topology

variable {Î± : Type*} {Î² : Type*} {Î³ : Type*} {Î´ : Type*}

/-- `i : Î± â†’ Î²` is "dense inducing" if it has dense range and the topology on `Î±`
  is the one induced by `i` from the topology on `Î²`. -/
structure IsDenseInducing [TopologicalSpace Î±] [TopologicalSpace Î²] (i : Î± â†’ Î²) : Prop
    extends IsInducing i where
  /-- The range of a dense inducing map is a dense set. -/
  protected dense : DenseRange i

namespace IsDenseInducing

variable [TopologicalSpace Î±] [TopologicalSpace Î²]

theorem _root_.Dense.isDenseInducing_val {s : Set Î±} (hs : Dense s) :
    IsDenseInducing (@Subtype.val Î± s) := âŸ¨IsInducing.subtypeVal, hs.denseRange_valâŸ©

variable {i : Î± â†’ Î²}

lemma isInducing (di : IsDenseInducing i) : IsInducing i := di.toIsInducing

theorem nhds_eq_comap (di : IsDenseInducing i) : âˆ€ a : Î±, ğ“ a = comap i (ğ“ <| i a) :=
  di.isInducing.nhds_eq_comap

protected theorem continuous (di : IsDenseInducing i) : Continuous i :=
  di.isInducing.continuous

theorem closure_range (di : IsDenseInducing i) : closure (range i) = univ :=
  di.dense.closure_range

protected theorem preconnectedSpace [PreconnectedSpace Î±] (di : IsDenseInducing i) :
    PreconnectedSpace Î² :=
  di.dense.preconnectedSpace di.continuous

theorem closure_image_mem_nhds {s : Set Î±} {a : Î±} (di : IsDenseInducing i) (hs : s âˆˆ ğ“ a) :
    closure (i '' s) âˆˆ ğ“ (i a) := by
  rw [di.nhds_eq_comap a, ((nhds_basis_opens _).comap _).mem_iff] at hs
  rcases hs with âŸ¨U, âŸ¨haU, hUoâŸ©, sub : i â»Â¹' U âŠ† sâŸ©
  refine mem_of_superset (hUo.mem_nhds haU) ?_
  calc
    U âŠ† closure (i '' (i â»Â¹' U)) := di.dense.subset_closure_image_preimage_of_isOpen hUo
    _ âŠ† closure (i '' s) := closure_mono (image_subset i sub)

theorem dense_image (di : IsDenseInducing i) {s : Set Î±} : Dense (i '' s) â†” Dense s := by
  refine âŸ¨fun H x => ?_, di.dense.dense_image di.continuousâŸ©
  rw [di.isInducing.closure_eq_preimage_closure_image, H.closure_eq, preimage_univ]
  trivial

/-- If `i : Î± â†’ Î²` is a dense embedding with dense complement of the range, then any compact set in
`Î±` has empty interior. -/
theorem interior_compact_eq_empty [T2Space Î²] (di : IsDenseInducing i) (hd : Dense (range i)á¶œ)
    {s : Set Î±} (hs : IsCompact s) : interior s = âˆ… := by
  refine eq_empty_iff_forall_notMem.2 fun x hx => ?_
  rw [mem_interior_iff_mem_nhds] at hx
  have := di.closure_image_mem_nhds hx
  rw [(hs.image di.continuous).isClosed.closure_eq] at this
  rcases hd.inter_nhds_nonempty this with âŸ¨y, hyi, hysâŸ©
  exact hyi (image_subset_range _ _ hys)

/-- The product of two dense inducings is a dense inducing -/
protected theorem prodMap [TopologicalSpace Î³] [TopologicalSpace Î´] {eâ‚ : Î± â†’ Î²} {eâ‚‚ : Î³ â†’ Î´}
    (deâ‚ : IsDenseInducing eâ‚) (deâ‚‚ : IsDenseInducing eâ‚‚) :
    IsDenseInducing (Prod.map eâ‚ eâ‚‚) where
  toIsInducing := deâ‚.isInducing.prodMap deâ‚‚.isInducing
  dense := deâ‚.dense.prodMap deâ‚‚.dense

open TopologicalSpace

/-- If the domain of a `IsDenseInducing` map is a separable space, then so is the codomain. -/
protected theorem separableSpace [SeparableSpace Î±] (di : IsDenseInducing i) : SeparableSpace Î² :=
  di.dense.separableSpace di.continuous

variable [TopologicalSpace Î´] {f : Î³ â†’ Î±} {g : Î³ â†’ Î´} {h : Î´ â†’ Î²}

/--
```
 Î³ -fâ†’ Î±
gâ†“     â†“e
 Î´ -hâ†’ Î²
```
-/
theorem tendsto_comap_nhds_nhds {d : Î´} {a : Î±} (di : IsDenseInducing i)
    (H : Tendsto h (ğ“ d) (ğ“ (i a))) (comm : h âˆ˜ g = i âˆ˜ f) : Tendsto f (comap g (ğ“ d)) (ğ“ a) := by
  have lim1 : map g (comap g (ğ“ d)) â‰¤ ğ“ d := map_comap_le
  replace lim1 : map h (map g (comap g (ğ“ d))) â‰¤ map h (ğ“ d) := map_mono lim1
  rw [Filter.map_map, comm, â† Filter.map_map, map_le_iff_le_comap] at lim1
  have lim2 : comap i (map h (ğ“ d)) â‰¤ comap i (ğ“ (i a)) := comap_mono H
  rw [â† di.nhds_eq_comap] at lim2
  exact le_trans lim1 lim2

protected theorem nhdsWithin_neBot (di : IsDenseInducing i) (b : Î²) : NeBot (ğ“[range i] b) :=
  di.dense.nhdsWithin_neBot b

theorem comap_nhds_neBot (di : IsDenseInducing i) (b : Î²) : NeBot (comap i (ğ“ b)) :=
  comap_neBot fun s hs => by
    rcases mem_closure_iff_nhds.1 (di.dense b) s hs with âŸ¨_, âŸ¨ha, a, rflâŸ©âŸ©
    exact âŸ¨a, haâŸ©

variable [TopologicalSpace Î³]

/-- If `i : Î± â†’ Î²` is a dense inducing, then any function `f : Î± â†’ Î³` "extends" to a function `g =
  IsDenseInducing.extend di f : Î² â†’ Î³`. If `Î³` is Hausdorff and `f` has a continuous extension, then
  `g` is the unique such extension. In general, `g` might not be continuous or even extend `f`. -/
def extend (di : IsDenseInducing i) (f : Î± â†’ Î³) (b : Î²) : Î³ :=
  @limUnder _ _ _ âŸ¨f (di.dense.some b)âŸ© (comap i (ğ“ b)) f

theorem extend_eq_of_tendsto [T2Space Î³] (di : IsDenseInducing i) {b : Î²} {c : Î³} {f : Î± â†’ Î³}
    (hf : Tendsto f (comap i (ğ“ b)) (ğ“ c)) : di.extend f b = c :=
  haveI := di.comap_nhds_neBot
  hf.limUnder_eq

theorem extend_eq_at [T2Space Î³] (di : IsDenseInducing i) {f : Î± â†’ Î³} {a : Î±}
    (hf : ContinuousAt f a) : di.extend f (i a) = f a :=
  extend_eq_of_tendsto _ <| di.nhds_eq_comap a â–¸ hf

theorem extend_eq_at' [T2Space Î³] (di : IsDenseInducing i) {f : Î± â†’ Î³} {a : Î±} (c : Î³)
    (hf : Tendsto f (ğ“ a) (ğ“ c)) : di.extend f (i a) = f a :=
  di.extend_eq_at (continuousAt_of_tendsto_nhds hf)

theorem extend_eq [T2Space Î³] (di : IsDenseInducing i) {f : Î± â†’ Î³} (hf : Continuous f) (a : Î±) :
    di.extend f (i a) = f a :=
  di.extend_eq_at hf.continuousAt

/-- Variation of `extend_eq` where we ask that `f` has a limit along `comap i (ğ“ b)` for each
`b : Î²`. This is a strictly stronger assumption than continuity of `f`, but in a lot of cases
you'd have to prove it anyway to use `continuous_extend`, so this avoids doing the work twice. -/
theorem extend_eq' [T2Space Î³] {f : Î± â†’ Î³} (di : IsDenseInducing i)
    (hf : âˆ€ b, âˆƒ c, Tendsto f (comap i (ğ“ b)) (ğ“ c)) (a : Î±) : di.extend f (i a) = f a := by
  rcases hf (i a) with âŸ¨b, hbâŸ©
  refine di.extend_eq_at' b ?_
  rwa [â† di.isInducing.nhds_eq_comap] at hb

theorem extend_unique_at [T2Space Î³] {b : Î²} {f : Î± â†’ Î³} {g : Î² â†’ Î³} (di : IsDenseInducing i)
    (hf : âˆ€á¶  x in comap i (ğ“ b), g (i x) = f x) (hg : ContinuousAt g b) : di.extend f b = g b := by
  refine di.extend_eq_of_tendsto fun s hs => mem_map.2 ?_
  suffices âˆ€á¶  x : Î± in comap i (ğ“ b), g (i x) âˆˆ s from
    hf.mp (this.mono fun x hgx hfx => hfx â–¸ hgx)
  clear hf f
  refine eventually_comap.2 ((hg.eventually hs).mono ?_)
  rintro _ hxs x rfl
  exact hxs

theorem extend_unique [T2Space Î³] {f : Î± â†’ Î³} {g : Î² â†’ Î³} (di : IsDenseInducing i)
    (hf : âˆ€ x, g (i x) = f x) (hg : Continuous g) : di.extend f = g :=
  funext fun _ => extend_unique_at di (Eventually.of_forall hf) hg.continuousAt

theorem continuousAt_extend [T3Space Î³] {b : Î²} {f : Î± â†’ Î³} (di : IsDenseInducing i)
    (hf : âˆ€á¶  x in ğ“ b, âˆƒ c, Tendsto f (comap i <| ğ“ x) (ğ“ c)) : ContinuousAt (di.extend f) b := by
  set Ï† := di.extend f
  haveI := di.comap_nhds_neBot
  suffices âˆ€ V' âˆˆ ğ“ (Ï† b), IsClosed V' â†’ Ï† â»Â¹' V' âˆˆ ğ“ b by
    simpa [ContinuousAt, (closed_nhds_basis (Ï† b)).tendsto_right_iff]
  intro V' V'_in V'_closed
  set Vâ‚ := { x | Tendsto f (comap i <| ğ“ x) (ğ“ <| Ï† x) }
  have Vâ‚_in : Vâ‚ âˆˆ ğ“ b := by
    filter_upwards [hf]
    rintro x âŸ¨c, hcâŸ©
    rwa [â† di.extend_eq_of_tendsto hc] at hc
  obtain âŸ¨Vâ‚‚, Vâ‚‚_in, Vâ‚‚_op, hVâ‚‚âŸ© : âˆƒ Vâ‚‚ âˆˆ ğ“ b, IsOpen Vâ‚‚ âˆ§ âˆ€ x âˆˆ i â»Â¹' Vâ‚‚, f x âˆˆ V' := by
    simpa [and_assoc] using
      ((nhds_basis_opens' b).comap i).tendsto_left_iff.mp (mem_of_mem_nhds Vâ‚_in : b âˆˆ Vâ‚) V' V'_in
  suffices âˆ€ x âˆˆ Vâ‚ âˆ© Vâ‚‚, Ï† x âˆˆ V' by filter_upwards [inter_mem Vâ‚_in Vâ‚‚_in] using this
  rintro x âŸ¨x_inâ‚, x_inâ‚‚âŸ©
  have hVâ‚‚x : Vâ‚‚ âˆˆ ğ“ x := IsOpen.mem_nhds Vâ‚‚_op x_inâ‚‚
  apply V'_closed.mem_of_tendsto x_inâ‚
  use Vâ‚‚
  tauto

theorem continuous_extend [T3Space Î³] {f : Î± â†’ Î³} (di : IsDenseInducing i)
    (hf : âˆ€ b, âˆƒ c, Tendsto f (comap i (ğ“ b)) (ğ“ c)) : Continuous (di.extend f) :=
  continuous_iff_continuousAt.mpr fun _ => di.continuousAt_extend <| univ_mem' hf

theorem mk' (i : Î± â†’ Î²) (c : Continuous i) (dense : âˆ€ x, x âˆˆ closure (range i))
    (H : âˆ€ (a : Î±), âˆ€ s âˆˆ ğ“ a, âˆƒ t âˆˆ ğ“ (i a), âˆ€ b, i b âˆˆ t â†’ b âˆˆ s) : IsDenseInducing i where
  toIsInducing := isInducing_iff_nhds.2 fun a =>
      le_antisymm (c.tendsto _).le_comap (by simpa [Filter.le_def] using H a)
  dense := dense

end IsDenseInducing

namespace Dense

variable [TopologicalSpace Î±] [TopologicalSpace Î²] {s : Set Î±}

/-- This is a shortcut for `hs.isDenseInducing_val.extend f`. It is useful because if `s : Set Î±`
is dense then the coercion `(â†‘) : s â†’ Î±` automatically satisfies `IsUniformInducing` and
`IsDenseInducing` so this gives access to the theorems satisfied by a uniform extension by simply
mentioning the density hypothesis. -/
noncomputable def extend (hs : Dense s) (f : s â†’ Î²) : Î± â†’ Î² :=
    hs.isDenseInducing_val.extend f

variable {f : s â†’ Î²}

theorem extend_eq_of_tendsto [T2Space Î²] (hs : Dense s) {a : Î±} {b : Î²}
    (hf : Tendsto f (comap (â†‘) (ğ“ a)) (ğ“ b)) : hs.extend f a = b :=
  hs.isDenseInducing_val.extend_eq_of_tendsto hf

theorem extend_eq_at [T2Space Î²] (hs : Dense s) {f : s â†’ Î²} {x : s}
    (hf : ContinuousAt f x) : hs.extend f x = f x :=
  hs.isDenseInducing_val.extend_eq_at hf

theorem extend_eq [T2Space Î²] (hs : Dense s) (hf : Continuous f) (x : s) :
    hs.extend f x = f x :=
  hs.extend_eq_at hf.continuousAt

theorem extend_unique_at [T2Space Î²] {a : Î±} {g : Î± â†’ Î²} (hs : Dense s)
    (hf : âˆ€á¶  x : s in comap (â†‘) (ğ“ a), g x = f x) (hg : ContinuousAt g a) :
    hs.extend f a = g a :=
  hs.isDenseInducing_val.extend_unique_at hf hg

theorem extend_unique [T2Space Î²] {g : Î± â†’ Î²} (hs : Dense s)
    (hf : âˆ€ x : s, g x = f x) (hg : Continuous g) : hs.extend f = g :=
  hs.isDenseInducing_val.extend_unique hf hg

theorem continuousAt_extend [T3Space Î²] {a : Î±} (hs : Dense s)
    (hf : âˆ€á¶  x in ğ“ a, âˆƒ b, Tendsto f (comap (â†‘) <| ğ“ x) (ğ“ b)) :
    ContinuousAt (hs.extend f) a :=
  hs.isDenseInducing_val.continuousAt_extend hf

theorem continuous_extend [T3Space Î²] (hs : Dense s)
    (hf : âˆ€ a : Î±, âˆƒ b, Tendsto f (comap (â†‘) (ğ“ a)) (ğ“ b)) : Continuous (hs.extend f) :=
  hs.isDenseInducing_val.continuous_extend hf

end Dense

/-- A dense embedding is an embedding with dense image. -/
structure IsDenseEmbedding [TopologicalSpace Î±] [TopologicalSpace Î²] (e : Î± â†’ Î²) : Prop
    extends IsDenseInducing e where
  /-- A dense embedding is injective. -/
  injective : Function.Injective e

lemma IsDenseEmbedding.mk' [TopologicalSpace Î±] [TopologicalSpace Î²] (e : Î± â†’ Î²) (c : Continuous e)
    (dense : DenseRange e) (injective : Function.Injective e)
    (H : âˆ€ (a : Î±), âˆ€ s âˆˆ ğ“ a, âˆƒ t âˆˆ ğ“ (e a), âˆ€ b, e b âˆˆ t â†’ b âˆˆ s) : IsDenseEmbedding e :=
  { IsDenseInducing.mk' e c dense H with injective }

namespace IsDenseEmbedding

open TopologicalSpace

variable [TopologicalSpace Î±] [TopologicalSpace Î²] [TopologicalSpace Î³] [TopologicalSpace Î´]
variable {e : Î± â†’ Î²}

lemma isDenseInducing (de : IsDenseEmbedding e) : IsDenseInducing e := de.toIsDenseInducing

theorem inj_iff (de : IsDenseEmbedding e) {x y} : e x = e y â†” x = y :=
  de.injective.eq_iff

theorem isEmbedding (de : IsDenseEmbedding e) : IsEmbedding e where __ := de

@[deprecated (since := "2024-10-26")]
alias to_embedding := isEmbedding

/-- If the domain of a `IsDenseEmbedding` is a separable space, then so is its codomain. -/
protected theorem separableSpace [SeparableSpace Î±] (de : IsDenseEmbedding e) : SeparableSpace Î² :=
  de.isDenseInducing.separableSpace

/-- The product of two dense embeddings is a dense embedding. -/
protected theorem prodMap {eâ‚ : Î± â†’ Î²} {eâ‚‚ : Î³ â†’ Î´} (deâ‚ : IsDenseEmbedding eâ‚)
    (deâ‚‚ : IsDenseEmbedding eâ‚‚) : IsDenseEmbedding fun p : Î± Ã— Î³ => (eâ‚ p.1, eâ‚‚ p.2) where
  toIsDenseInducing := deâ‚.isDenseInducing.prodMap deâ‚‚.isDenseInducing
  injective := deâ‚.injective.prodMap deâ‚‚.injective

/-- The dense embedding of a subtype inside its closure. -/
@[simps]
def subtypeEmb {Î± : Type*} (p : Î± â†’ Prop) (e : Î± â†’ Î²) (x : { x // p x }) :
    { x // x âˆˆ closure (e '' { x | p x }) } :=
  âŸ¨e x, subset_closure <| mem_image_of_mem e x.propâŸ©

protected theorem subtype (de : IsDenseEmbedding e) (p : Î± â†’ Prop) :
    IsDenseEmbedding (subtypeEmb p e) where
  dense :=
    dense_iff_closure_eq.2 <| by
      ext âŸ¨x, hxâŸ©
      rw [image_eq_range] at hx
      simpa [closure_subtype, â† range_comp, (Â· âˆ˜ Â·)]
  injective := (de.injective.comp Subtype.coe_injective).codRestrict _
  eq_induced :=
    (induced_iff_nhds_eq _).2 fun âŸ¨x, hxâŸ© => by
      simp [subtypeEmb, nhds_subtype_eq_comap, de.isInducing.nhds_eq_comap, comap_comap,
        Function.comp_def]

theorem dense_image (de : IsDenseEmbedding e) {s : Set Î±} : Dense (e '' s) â†” Dense s :=
  de.isDenseInducing.dense_image

protected lemma id {Î± : Type*} [TopologicalSpace Î±] : IsDenseEmbedding (id : Î± â†’ Î±) :=
  { IsEmbedding.id with dense := denseRange_id }

end IsDenseEmbedding

theorem Dense.isDenseEmbedding_val [TopologicalSpace Î±] {s : Set Î±} (hs : Dense s) :
    IsDenseEmbedding ((â†‘) : s â†’ Î±) :=
  { IsEmbedding.subtypeVal with dense := hs.denseRange_val }

theorem isClosed_property [TopologicalSpace Î²] {e : Î± â†’ Î²} {p : Î² â†’ Prop} (he : DenseRange e)
    (hp : IsClosed { x | p x }) (h : âˆ€ a, p (e a)) : âˆ€ b, p b := by
  have : univ âŠ† { b | p b } :=
    calc
      univ = closure (range e) := he.closure_range.symm
      _ âŠ† closure { b | p b } := closure_mono <| range_subset_iff.mpr h
      _ = _ := hp.closure_eq
  simpa only [univ_subset_iff, eq_univ_iff_forall, mem_setOf]

theorem isClosed_property2 [TopologicalSpace Î²] {e : Î± â†’ Î²} {p : Î² â†’ Î² â†’ Prop} (he : DenseRange e)
    (hp : IsClosed { q : Î² Ã— Î² | p q.1 q.2 }) (h : âˆ€ aâ‚ aâ‚‚, p (e aâ‚) (e aâ‚‚)) : âˆ€ bâ‚ bâ‚‚, p bâ‚ bâ‚‚ :=
  have : âˆ€ q : Î² Ã— Î², p q.1 q.2 := isClosed_property (he.prodMap he) hp fun _ => h _ _
  fun bâ‚ bâ‚‚ => this âŸ¨bâ‚, bâ‚‚âŸ©

theorem isClosed_property3 [TopologicalSpace Î²] {e : Î± â†’ Î²} {p : Î² â†’ Î² â†’ Î² â†’ Prop}
    (he : DenseRange e) (hp : IsClosed { q : Î² Ã— Î² Ã— Î² | p q.1 q.2.1 q.2.2 })
    (h : âˆ€ aâ‚ aâ‚‚ aâ‚ƒ, p (e aâ‚) (e aâ‚‚) (e aâ‚ƒ)) : âˆ€ bâ‚ bâ‚‚ bâ‚ƒ, p bâ‚ bâ‚‚ bâ‚ƒ :=
  have : âˆ€ q : Î² Ã— Î² Ã— Î², p q.1 q.2.1 q.2.2 :=
    isClosed_property (he.prodMap <| he.prodMap he) hp fun _ => h _ _ _
  fun bâ‚ bâ‚‚ bâ‚ƒ => this âŸ¨bâ‚, bâ‚‚, bâ‚ƒâŸ©

@[elab_as_elim]
theorem DenseRange.induction_on [TopologicalSpace Î²] {e : Î± â†’ Î²} (he : DenseRange e) {p : Î² â†’ Prop}
    (bâ‚€ : Î²) (hp : IsClosed { b | p b }) (ih : âˆ€ a : Î±, p <| e a) : p bâ‚€ :=
  isClosed_property he hp ih bâ‚€

@[elab_as_elim]
theorem DenseRange.induction_onâ‚‚ [TopologicalSpace Î²] {e : Î± â†’ Î²} {p : Î² â†’ Î² â†’ Prop}
    (he : DenseRange e) (hp : IsClosed { q : Î² Ã— Î² | p q.1 q.2 }) (h : âˆ€ aâ‚ aâ‚‚, p (e aâ‚) (e aâ‚‚))
    (bâ‚ bâ‚‚ : Î²) : p bâ‚ bâ‚‚ :=
  isClosed_property2 he hp h _ _

@[elab_as_elim]
theorem DenseRange.induction_onâ‚ƒ [TopologicalSpace Î²] {e : Î± â†’ Î²} {p : Î² â†’ Î² â†’ Î² â†’ Prop}
    (he : DenseRange e) (hp : IsClosed { q : Î² Ã— Î² Ã— Î² | p q.1 q.2.1 q.2.2 })
    (h : âˆ€ aâ‚ aâ‚‚ aâ‚ƒ, p (e aâ‚) (e aâ‚‚) (e aâ‚ƒ)) (bâ‚ bâ‚‚ bâ‚ƒ : Î²) : p bâ‚ bâ‚‚ bâ‚ƒ :=
  isClosed_property3 he hp h _ _ _

section

variable [TopologicalSpace Î²] [TopologicalSpace Î³] [T2Space Î³]
variable {f : Î± â†’ Î²}

/-- Two continuous functions to a t2-space that agree on the dense range of a function are equal. -/
theorem DenseRange.equalizer (hfd : DenseRange f) {g h : Î² â†’ Î³} (hg : Continuous g)
    (hh : Continuous h) (H : g âˆ˜ f = h âˆ˜ f) : g = h :=
  funext fun y => hfd.induction_on y (isClosed_eq hg hh) <| congr_fun H

end

-- Bourbaki GT III Â§3 no.4 Proposition 7 (generalised to any dense-inducing map to a regular space)
theorem Filter.HasBasis.hasBasis_of_isDenseInducing [TopologicalSpace Î±] [TopologicalSpace Î²]
    [RegularSpace Î²] {Î¹ : Type*} {s : Î¹ â†’ Set Î±} {p : Î¹ â†’ Prop} {x : Î±} (h : (ğ“ x).HasBasis p s)
    {f : Î± â†’ Î²} (hf : IsDenseInducing f) : (ğ“ (f x)).HasBasis p fun i => closure <| f '' s i := by
  rw [Filter.hasBasis_iff] at h âŠ¢
  intro T
  refine âŸ¨fun hT => ?_, fun hT => ?_âŸ©
  Â· obtain âŸ¨T', hTâ‚, hTâ‚‚, hTâ‚ƒâŸ© := exists_mem_nhds_isClosed_subset hT
    have hTâ‚„ : f â»Â¹' T' âˆˆ ğ“ x := by
      rw [hf.isInducing.nhds_eq_comap x]
      exact âŸ¨T', hTâ‚, Subset.rflâŸ©
    obtain âŸ¨i, hi, hi'âŸ© := (h _).mp hTâ‚„
    exact
      âŸ¨i, hi,
        (closure_mono (image_subset f hi')).trans
          (Subset.trans (closure_minimal (image_preimage_subset _ _) hTâ‚‚) hTâ‚ƒ)âŸ©
  Â· obtain âŸ¨i, hi, hi'âŸ© := hT
    suffices closure (f '' s i) âˆˆ ğ“ (f x) by filter_upwards [this] using hi'
    replace h := (h (s i)).mpr âŸ¨i, hi, Subset.rflâŸ©
    exact hf.closure_image_mem_nhds h
