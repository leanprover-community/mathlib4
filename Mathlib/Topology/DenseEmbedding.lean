/-
Copyright (c) 2019 Reid Barton. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro, Patrick Massot
-/
import Mathlib.Topology.Bases
import Mathlib.Topology.Separation.Basic

/-!
# Dense embeddings

This file defines three properties of functions:

* `DenseRange f`       means `f` has dense image;
* `IsDenseInducing i`  means `i` is also inducing, namely it induces the topology on its codomain;
* `IsDenseEmbedding e` means `e` is further an embedding, namely it is injective and `Inducing`.

The main theorem `continuous_extend` gives a criterion for a function
`f : X ‚Üí Z` to a T‚ÇÉ space Z to extend along a dense embedding
`i : X ‚Üí Y` to a continuous function `g : Y ‚Üí Z`. Actually `i` only
has to be `IsDenseInducing` (not necessarily injective).

-/


noncomputable section

open Set Filter
open scoped Topology

variable {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*} {Œ¥ : Type*}

/-- `i : Œ± ‚Üí Œ≤` is "dense inducing" if it has dense range and the topology on `Œ±`
  is the one induced by `i` from the topology on `Œ≤`. -/
structure IsDenseInducing [TopologicalSpace Œ±] [TopologicalSpace Œ≤] (i : Œ± ‚Üí Œ≤)
    extends IsInducing i : Prop where
  /-- The range of a dense inducing map is a dense set. -/
  protected dense : DenseRange i

namespace IsDenseInducing

variable [TopologicalSpace Œ±] [TopologicalSpace Œ≤]
variable {i : Œ± ‚Üí Œ≤}

lemma isInducing (di : IsDenseInducing i) : IsInducing i := di.toIsInducing

theorem nhds_eq_comap (di : IsDenseInducing i) : ‚àÄ a : Œ±, ùìù a = comap i (ùìù <| i a) :=
  di.isInducing.nhds_eq_comap

protected theorem continuous (di : IsDenseInducing i) : Continuous i :=
  di.isInducing.continuous

theorem closure_range (di : IsDenseInducing i) : closure (range i) = univ :=
  di.dense.closure_range

protected theorem preconnectedSpace [PreconnectedSpace Œ±] (di : IsDenseInducing i) :
    PreconnectedSpace Œ≤ :=
  di.dense.preconnectedSpace di.continuous

theorem closure_image_mem_nhds {s : Set Œ±} {a : Œ±} (di : IsDenseInducing i) (hs : s ‚àà ùìù a) :
    closure (i '' s) ‚àà ùìù (i a) := by
  rw [di.nhds_eq_comap a, ((nhds_basis_opens _).comap _).mem_iff] at hs
  rcases hs with ‚ü®U, ‚ü®haU, hUo‚ü©, sub : i ‚Åª¬π' U ‚äÜ s‚ü©
  refine mem_of_superset (hUo.mem_nhds haU) ?_
  calc
    U ‚äÜ closure (i '' (i ‚Åª¬π' U)) := di.dense.subset_closure_image_preimage_of_isOpen hUo
    _ ‚äÜ closure (i '' s) := closure_mono (image_subset i sub)

theorem dense_image (di : IsDenseInducing i) {s : Set Œ±} : Dense (i '' s) ‚Üî Dense s := by
  refine ‚ü®fun H x => ?_, di.dense.dense_image di.continuous‚ü©
  rw [di.isInducing.closure_eq_preimage_closure_image, H.closure_eq, preimage_univ]
  trivial

/-- If `i : Œ± ‚Üí Œ≤` is a dense embedding with dense complement of the range, then any compact set in
`Œ±` has empty interior. -/
theorem interior_compact_eq_empty [T2Space Œ≤] (di : IsDenseInducing i) (hd : Dense (range i)·∂ú)
    {s : Set Œ±} (hs : IsCompact s) : interior s = ‚àÖ := by
  refine eq_empty_iff_forall_not_mem.2 fun x hx => ?_
  rw [mem_interior_iff_mem_nhds] at hx
  have := di.closure_image_mem_nhds hx
  rw [(hs.image di.continuous).isClosed.closure_eq] at this
  rcases hd.inter_nhds_nonempty this with ‚ü®y, hyi, hys‚ü©
  exact hyi (image_subset_range _ _ hys)

/-- The product of two dense inducings is a dense inducing -/
protected theorem prodMap [TopologicalSpace Œ≥] [TopologicalSpace Œ¥] {e‚ÇÅ : Œ± ‚Üí Œ≤} {e‚ÇÇ : Œ≥ ‚Üí Œ¥}
    (de‚ÇÅ : IsDenseInducing e‚ÇÅ) (de‚ÇÇ : IsDenseInducing e‚ÇÇ) :
    IsDenseInducing (Prod.map e‚ÇÅ e‚ÇÇ) where
  toIsInducing := de‚ÇÅ.isInducing.prodMap de‚ÇÇ.isInducing
  dense := de‚ÇÅ.dense.prodMap de‚ÇÇ.dense

@[deprecated (since := "2024-10-06")]
protected alias prod := IsDenseInducing.prodMap

open TopologicalSpace

/-- If the domain of a `IsDenseInducing` map is a separable space, then so is the codomain. -/
protected theorem separableSpace [SeparableSpace Œ±] (di : IsDenseInducing i) : SeparableSpace Œ≤ :=
  di.dense.separableSpace di.continuous

variable [TopologicalSpace Œ¥] {f : Œ≥ ‚Üí Œ±} {g : Œ≥ ‚Üí Œ¥} {h : Œ¥ ‚Üí Œ≤}

/--
```
 Œ≥ -f‚Üí Œ±
g‚Üì     ‚Üìe
 Œ¥ -h‚Üí Œ≤
```
-/
theorem tendsto_comap_nhds_nhds {d : Œ¥} {a : Œ±} (di : IsDenseInducing i)
    (H : Tendsto h (ùìù d) (ùìù (i a))) (comm : h ‚àò g = i ‚àò f) : Tendsto f (comap g (ùìù d)) (ùìù a) := by
  have lim1 : map g (comap g (ùìù d)) ‚â§ ùìù d := map_comap_le
  replace lim1 : map h (map g (comap g (ùìù d))) ‚â§ map h (ùìù d) := map_mono lim1
  rw [Filter.map_map, comm, ‚Üê Filter.map_map, map_le_iff_le_comap] at lim1
  have lim2 : comap i (map h (ùìù d)) ‚â§ comap i (ùìù (i a)) := comap_mono H
  rw [‚Üê di.nhds_eq_comap] at lim2
  exact le_trans lim1 lim2

protected theorem nhdsWithin_neBot (di : IsDenseInducing i) (b : Œ≤) : NeBot (ùìù[range i] b) :=
  di.dense.nhdsWithin_neBot b

theorem comap_nhds_neBot (di : IsDenseInducing i) (b : Œ≤) : NeBot (comap i (ùìù b)) :=
  comap_neBot fun s hs => by
    rcases mem_closure_iff_nhds.1 (di.dense b) s hs with ‚ü®_, ‚ü®ha, a, rfl‚ü©‚ü©
    exact ‚ü®a, ha‚ü©

variable [TopologicalSpace Œ≥]

/-- If `i : Œ± ‚Üí Œ≤` is a dense inducing, then any function `f : Œ± ‚Üí Œ≥` "extends" to a function `g =
  IsDenseInducing.extend di f : Œ≤ ‚Üí Œ≥`. If `Œ≥` is Hausdorff and `f` has a continuous extension, then
  `g` is the unique such extension. In general, `g` might not be continuous or even extend `f`. -/
def extend (di : IsDenseInducing i) (f : Œ± ‚Üí Œ≥) (b : Œ≤) : Œ≥ :=
  @limUnder _ _ _ ‚ü®f (di.dense.some b)‚ü© (comap i (ùìù b)) f

theorem extend_eq_of_tendsto [T2Space Œ≥] (di : IsDenseInducing i) {b : Œ≤} {c : Œ≥} {f : Œ± ‚Üí Œ≥}
    (hf : Tendsto f (comap i (ùìù b)) (ùìù c)) : di.extend f b = c :=
  haveI := di.comap_nhds_neBot
  hf.limUnder_eq

theorem extend_eq_at [T2Space Œ≥] (di : IsDenseInducing i) {f : Œ± ‚Üí Œ≥} {a : Œ±}
    (hf : ContinuousAt f a) : di.extend f (i a) = f a :=
  extend_eq_of_tendsto _ <| di.nhds_eq_comap a ‚ñ∏ hf

theorem extend_eq_at' [T2Space Œ≥] (di : IsDenseInducing i) {f : Œ± ‚Üí Œ≥} {a : Œ±} (c : Œ≥)
    (hf : Tendsto f (ùìù a) (ùìù c)) : di.extend f (i a) = f a :=
  di.extend_eq_at (continuousAt_of_tendsto_nhds hf)

theorem extend_eq [T2Space Œ≥] (di : IsDenseInducing i) {f : Œ± ‚Üí Œ≥} (hf : Continuous f) (a : Œ±) :
    di.extend f (i a) = f a :=
  di.extend_eq_at hf.continuousAt

/-- Variation of `extend_eq` where we ask that `f` has a limit along `comap i (ùìù b)` for each
`b : Œ≤`. This is a strictly stronger assumption than continuity of `f`, but in a lot of cases
you'd have to prove it anyway to use `continuous_extend`, so this avoids doing the work twice. -/
theorem extend_eq' [T2Space Œ≥] {f : Œ± ‚Üí Œ≥} (di : IsDenseInducing i)
    (hf : ‚àÄ b, ‚àÉ c, Tendsto f (comap i (ùìù b)) (ùìù c)) (a : Œ±) : di.extend f (i a) = f a := by
  rcases hf (i a) with ‚ü®b, hb‚ü©
  refine di.extend_eq_at' b ?_
  rwa [‚Üê di.isInducing.nhds_eq_comap] at hb

theorem extend_unique_at [T2Space Œ≥] {b : Œ≤} {f : Œ± ‚Üí Œ≥} {g : Œ≤ ‚Üí Œ≥} (di : IsDenseInducing i)
    (hf : ‚àÄ·∂† x in comap i (ùìù b), g (i x) = f x) (hg : ContinuousAt g b) : di.extend f b = g b := by
  refine di.extend_eq_of_tendsto fun s hs => mem_map.2 ?_
  suffices ‚àÄ·∂† x : Œ± in comap i (ùìù b), g (i x) ‚àà s from
    hf.mp (this.mono fun x hgx hfx => hfx ‚ñ∏ hgx)
  clear hf f
  refine eventually_comap.2 ((hg.eventually hs).mono ?_)
  rintro _ hxs x rfl
  exact hxs

theorem extend_unique [T2Space Œ≥] {f : Œ± ‚Üí Œ≥} {g : Œ≤ ‚Üí Œ≥} (di : IsDenseInducing i)
    (hf : ‚àÄ x, g (i x) = f x) (hg : Continuous g) : di.extend f = g :=
  funext fun _ => extend_unique_at di (Eventually.of_forall hf) hg.continuousAt

theorem continuousAt_extend [T3Space Œ≥] {b : Œ≤} {f : Œ± ‚Üí Œ≥} (di : IsDenseInducing i)
    (hf : ‚àÄ·∂† x in ùìù b, ‚àÉ c, Tendsto f (comap i <| ùìù x) (ùìù c)) : ContinuousAt (di.extend f) b := by
  set œÜ := di.extend f
  haveI := di.comap_nhds_neBot
  suffices ‚àÄ V' ‚àà ùìù (œÜ b), IsClosed V' ‚Üí œÜ ‚Åª¬π' V' ‚àà ùìù b by
    simpa [ContinuousAt, (closed_nhds_basis (œÜ b)).tendsto_right_iff]
  intro V' V'_in V'_closed
  set V‚ÇÅ := { x | Tendsto f (comap i <| ùìù x) (ùìù <| œÜ x) }
  have V‚ÇÅ_in : V‚ÇÅ ‚àà ùìù b := by
    filter_upwards [hf]
    rintro x ‚ü®c, hc‚ü©
    rwa [‚Üê di.extend_eq_of_tendsto hc] at hc
  obtain ‚ü®V‚ÇÇ, V‚ÇÇ_in, V‚ÇÇ_op, hV‚ÇÇ‚ü© : ‚àÉ V‚ÇÇ ‚àà ùìù b, IsOpen V‚ÇÇ ‚àß ‚àÄ x ‚àà i ‚Åª¬π' V‚ÇÇ, f x ‚àà V' := by
    simpa [and_assoc] using
      ((nhds_basis_opens' b).comap i).tendsto_left_iff.mp (mem_of_mem_nhds V‚ÇÅ_in : b ‚àà V‚ÇÅ) V' V'_in
  suffices ‚àÄ x ‚àà V‚ÇÅ ‚à© V‚ÇÇ, œÜ x ‚àà V' by filter_upwards [inter_mem V‚ÇÅ_in V‚ÇÇ_in] using this
  rintro x ‚ü®x_in‚ÇÅ, x_in‚ÇÇ‚ü©
  have hV‚ÇÇx : V‚ÇÇ ‚àà ùìù x := IsOpen.mem_nhds V‚ÇÇ_op x_in‚ÇÇ
  apply V'_closed.mem_of_tendsto x_in‚ÇÅ
  use V‚ÇÇ
  tauto

theorem continuous_extend [T3Space Œ≥] {f : Œ± ‚Üí Œ≥} (di : IsDenseInducing i)
    (hf : ‚àÄ b, ‚àÉ c, Tendsto f (comap i (ùìù b)) (ùìù c)) : Continuous (di.extend f) :=
  continuous_iff_continuousAt.mpr fun _ => di.continuousAt_extend <| univ_mem' hf

theorem mk' (i : Œ± ‚Üí Œ≤) (c : Continuous i) (dense : ‚àÄ x, x ‚àà closure (range i))
    (H : ‚àÄ (a : Œ±), ‚àÄ s ‚àà ùìù a, ‚àÉ t ‚àà ùìù (i a), ‚àÄ b, i b ‚àà t ‚Üí b ‚àà s) : IsDenseInducing i where
  toIsInducing := isInducing_iff_nhds.2 fun a =>
      le_antisymm (c.tendsto _).le_comap (by simpa [Filter.le_def] using H a)
  dense := dense

end IsDenseInducing

/-- A dense embedding is an embedding with dense image. -/
structure IsDenseEmbedding [TopologicalSpace Œ±] [TopologicalSpace Œ≤] (e : Œ± ‚Üí Œ≤) extends
  IsDenseInducing e : Prop where
  /-- A dense embedding is injective. -/
  inj : Function.Injective e

lemma IsDenseEmbedding.mk' [TopologicalSpace Œ±] [TopologicalSpace Œ≤] (e : Œ± ‚Üí Œ≤) (c : Continuous e)
    (dense : DenseRange e) (inj : Function.Injective e)
    (H : ‚àÄ (a : Œ±), ‚àÄ s ‚àà ùìù a, ‚àÉ t ‚àà ùìù (e a), ‚àÄ b, e b ‚àà t ‚Üí b ‚àà s) : IsDenseEmbedding e :=
  { IsDenseInducing.mk' e c dense H with inj }

@[deprecated (since := "2024-09-30")]
alias DenseEmbedding.mk' := IsDenseEmbedding.mk'

namespace IsDenseEmbedding

open TopologicalSpace

variable [TopologicalSpace Œ±] [TopologicalSpace Œ≤] [TopologicalSpace Œ≥] [TopologicalSpace Œ¥]
variable {e : Œ± ‚Üí Œ≤}

theorem inj_iff (de : IsDenseEmbedding e) {x y} : e x = e y ‚Üî x = y :=
  de.inj.eq_iff

theorem isEmbedding (de : IsDenseEmbedding e) : IsEmbedding e where __ := de

@[deprecated (since := "2024-10-26")]
alias to_embedding := isEmbedding

/-- If the domain of a `IsDenseEmbedding` is a separable space, then so is its codomain. -/
protected theorem separableSpace [SeparableSpace Œ±] (de : IsDenseEmbedding e) : SeparableSpace Œ≤ :=
  de.toIsDenseInducing.separableSpace

/-- The product of two dense embeddings is a dense embedding. -/
protected theorem prodMap {e‚ÇÅ : Œ± ‚Üí Œ≤} {e‚ÇÇ : Œ≥ ‚Üí Œ¥} (de‚ÇÅ : IsDenseEmbedding e‚ÇÅ)
    (de‚ÇÇ : IsDenseEmbedding e‚ÇÇ) : IsDenseEmbedding fun p : Œ± √ó Œ≥ => (e‚ÇÅ p.1, e‚ÇÇ p.2) :=
  { de‚ÇÅ.toIsDenseInducing.prodMap de‚ÇÇ.toIsDenseInducing with
    inj := de‚ÇÅ.inj.prodMap de‚ÇÇ.inj }

@[deprecated (since := "2024-10-06")] protected alias prod := IsDenseEmbedding.prodMap

/-- The dense embedding of a subtype inside its closure. -/
@[simps]
def subtypeEmb {Œ± : Type*} (p : Œ± ‚Üí Prop) (e : Œ± ‚Üí Œ≤) (x : { x // p x }) :
    { x // x ‚àà closure (e '' { x | p x }) } :=
  ‚ü®e x, subset_closure <| mem_image_of_mem e x.prop‚ü©

protected theorem subtype (de : IsDenseEmbedding e) (p : Œ± ‚Üí Prop) :
    IsDenseEmbedding (subtypeEmb p e) where
  dense :=
    dense_iff_closure_eq.2 <| by
      ext ‚ü®x, hx‚ü©
      rw [image_eq_range] at hx
      simpa [closure_subtype, ‚Üê range_comp, (¬∑ ‚àò ¬∑)]
  inj := (de.inj.comp Subtype.coe_injective).codRestrict _
  eq_induced :=
    (induced_iff_nhds_eq _).2 fun ‚ü®x, hx‚ü© => by
      simp [subtypeEmb, nhds_subtype_eq_comap, de.isInducing.nhds_eq_comap, comap_comap,
        Function.comp_def]

theorem dense_image (de : IsDenseEmbedding e) {s : Set Œ±} : Dense (e '' s) ‚Üî Dense s :=
  de.toIsDenseInducing.dense_image

protected lemma id {Œ± : Type*} [TopologicalSpace Œ±] : IsDenseEmbedding (id : Œ± ‚Üí Œ±) :=
  { IsEmbedding.id with dense := denseRange_id }

end IsDenseEmbedding

@[deprecated (since := "2024-09-30")]
alias denseEmbedding_id := IsDenseEmbedding.id

theorem Dense.isDenseEmbedding_val [TopologicalSpace Œ±] {s : Set Œ±} (hs : Dense s) :
    IsDenseEmbedding ((‚Üë) : s ‚Üí Œ±) :=
  { IsEmbedding.subtypeVal with dense := hs.denseRange_val }

@[deprecated (since := "2024-09-30")]
alias Dense.denseEmbedding_val := Dense.isDenseEmbedding_val

theorem isClosed_property [TopologicalSpace Œ≤] {e : Œ± ‚Üí Œ≤} {p : Œ≤ ‚Üí Prop} (he : DenseRange e)
    (hp : IsClosed { x | p x }) (h : ‚àÄ a, p (e a)) : ‚àÄ b, p b :=
  have : univ ‚äÜ { b | p b } :=
    calc
      univ = closure (range e) := he.closure_range.symm
      _ ‚äÜ closure { b | p b } := closure_mono <| range_subset_iff.mpr h
      _ = _ := hp.closure_eq

  fun _ => this trivial

theorem isClosed_property2 [TopologicalSpace Œ≤] {e : Œ± ‚Üí Œ≤} {p : Œ≤ ‚Üí Œ≤ ‚Üí Prop} (he : DenseRange e)
    (hp : IsClosed { q : Œ≤ √ó Œ≤ | p q.1 q.2 }) (h : ‚àÄ a‚ÇÅ a‚ÇÇ, p (e a‚ÇÅ) (e a‚ÇÇ)) : ‚àÄ b‚ÇÅ b‚ÇÇ, p b‚ÇÅ b‚ÇÇ :=
  have : ‚àÄ q : Œ≤ √ó Œ≤, p q.1 q.2 := isClosed_property (he.prodMap he) hp fun _ => h _ _
  fun b‚ÇÅ b‚ÇÇ => this ‚ü®b‚ÇÅ, b‚ÇÇ‚ü©

theorem isClosed_property3 [TopologicalSpace Œ≤] {e : Œ± ‚Üí Œ≤} {p : Œ≤ ‚Üí Œ≤ ‚Üí Œ≤ ‚Üí Prop}
    (he : DenseRange e) (hp : IsClosed { q : Œ≤ √ó Œ≤ √ó Œ≤ | p q.1 q.2.1 q.2.2 })
    (h : ‚àÄ a‚ÇÅ a‚ÇÇ a‚ÇÉ, p (e a‚ÇÅ) (e a‚ÇÇ) (e a‚ÇÉ)) : ‚àÄ b‚ÇÅ b‚ÇÇ b‚ÇÉ, p b‚ÇÅ b‚ÇÇ b‚ÇÉ :=
  have : ‚àÄ q : Œ≤ √ó Œ≤ √ó Œ≤, p q.1 q.2.1 q.2.2 :=
    isClosed_property (he.prodMap <| he.prodMap he) hp fun _ => h _ _ _
  fun b‚ÇÅ b‚ÇÇ b‚ÇÉ => this ‚ü®b‚ÇÅ, b‚ÇÇ, b‚ÇÉ‚ü©

@[elab_as_elim]
theorem DenseRange.induction_on [TopologicalSpace Œ≤] {e : Œ± ‚Üí Œ≤} (he : DenseRange e) {p : Œ≤ ‚Üí Prop}
    (b‚ÇÄ : Œ≤) (hp : IsClosed { b | p b }) (ih : ‚àÄ a : Œ±, p <| e a) : p b‚ÇÄ :=
  isClosed_property he hp ih b‚ÇÄ

@[elab_as_elim]
theorem DenseRange.induction_on‚ÇÇ [TopologicalSpace Œ≤] {e : Œ± ‚Üí Œ≤} {p : Œ≤ ‚Üí Œ≤ ‚Üí Prop}
    (he : DenseRange e) (hp : IsClosed { q : Œ≤ √ó Œ≤ | p q.1 q.2 }) (h : ‚àÄ a‚ÇÅ a‚ÇÇ, p (e a‚ÇÅ) (e a‚ÇÇ))
    (b‚ÇÅ b‚ÇÇ : Œ≤) : p b‚ÇÅ b‚ÇÇ :=
  isClosed_property2 he hp h _ _

@[elab_as_elim]
theorem DenseRange.induction_on‚ÇÉ [TopologicalSpace Œ≤] {e : Œ± ‚Üí Œ≤} {p : Œ≤ ‚Üí Œ≤ ‚Üí Œ≤ ‚Üí Prop}
    (he : DenseRange e) (hp : IsClosed { q : Œ≤ √ó Œ≤ √ó Œ≤ | p q.1 q.2.1 q.2.2 })
    (h : ‚àÄ a‚ÇÅ a‚ÇÇ a‚ÇÉ, p (e a‚ÇÅ) (e a‚ÇÇ) (e a‚ÇÉ)) (b‚ÇÅ b‚ÇÇ b‚ÇÉ : Œ≤) : p b‚ÇÅ b‚ÇÇ b‚ÇÉ :=
  isClosed_property3 he hp h _ _ _

section

variable [TopologicalSpace Œ≤] [TopologicalSpace Œ≥] [T2Space Œ≥]
variable {f : Œ± ‚Üí Œ≤}

/-- Two continuous functions to a t2-space that agree on the dense range of a function are equal. -/
theorem DenseRange.equalizer (hfd : DenseRange f) {g h : Œ≤ ‚Üí Œ≥} (hg : Continuous g)
    (hh : Continuous h) (H : g ‚àò f = h ‚àò f) : g = h :=
  funext fun y => hfd.induction_on y (isClosed_eq hg hh) <| congr_fun H

end

-- Bourbaki GT III ¬ß3 no.4 Proposition 7 (generalised to any dense-inducing map to a T‚ÇÉ space)
theorem Filter.HasBasis.hasBasis_of_isDenseInducing [TopologicalSpace Œ±] [TopologicalSpace Œ≤]
    [T3Space Œ≤] {Œπ : Type*} {s : Œπ ‚Üí Set Œ±} {p : Œπ ‚Üí Prop} {x : Œ±} (h : (ùìù x).HasBasis p s)
    {f : Œ± ‚Üí Œ≤} (hf : IsDenseInducing f) : (ùìù (f x)).HasBasis p fun i => closure <| f '' s i := by
  rw [Filter.hasBasis_iff] at h ‚ä¢
  intro T
  refine ‚ü®fun hT => ?_, fun hT => ?_‚ü©
  ¬∑ obtain ‚ü®T', hT‚ÇÅ, hT‚ÇÇ, hT‚ÇÉ‚ü© := exists_mem_nhds_isClosed_subset hT
    have hT‚ÇÑ : f ‚Åª¬π' T' ‚àà ùìù x := by
      rw [hf.isInducing.nhds_eq_comap x]
      exact ‚ü®T', hT‚ÇÅ, Subset.rfl‚ü©
    obtain ‚ü®i, hi, hi'‚ü© := (h _).mp hT‚ÇÑ
    exact
      ‚ü®i, hi,
        (closure_mono (image_subset f hi')).trans
          (Subset.trans (closure_minimal (image_preimage_subset _ _) hT‚ÇÇ) hT‚ÇÉ)‚ü©
  ¬∑ obtain ‚ü®i, hi, hi'‚ü© := hT
    suffices closure (f '' s i) ‚àà ùìù (f x) by filter_upwards [this] using hi'
    replace h := (h (s i)).mpr ‚ü®i, hi, Subset.rfl‚ü©
    exact hf.closure_image_mem_nhds h
