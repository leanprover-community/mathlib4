/-
Copyright (c) 2023 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Data.Set.Image
import Mathlib.Topology.Bases
import Mathlib.Topology.Inseparable
import Mathlib.Topology.Compactness.Compact

/-!
# Alexandrov-discrete topological spaces

This file defines Alexandrov-discrete spaces, aka finitely generated spaces.

A space is Alexandrov-discrete if the (arbitrary) intersection of open sets is open. As such,
the intersection of all neighborhoods of a set is a neighborhood itself. Hence every set has a
minimal neighborhood, which we call the *exterior* of the set.

## Main declarations

* `AlexandrovDiscrete`: Prop-valued typeclass for a topological space to be Alexandrov-discrete
* `exterior`: Intersection of all neighborhoods of a set. When the space is Alexandrov-discrete,
  this is the minimal neighborhood of the set.

## Notes

The "minimal neighborhood of a set" construction is not named in the literature. We chose the name
"exterior" with analogy to the interior. `interior` and `exterior` have the same properties up to


## TODO

Finite product of Alexandrov-discrete spaces is Alexandrov-discrete.

## Tags

Alexandroff, discrete, finitely generated, fg space
-/

open Filter Set TopologicalSpace
open scoped Topology

/-- A topological space is **Alexandrov-discrete** or **finitely generated** if the intersection of
a family of open sets is open. -/
class AlexandrovDiscrete (Î± : Type*) [TopologicalSpace Î±] : Prop where
  /-- The intersection of a family of open sets is an open set. Use `isOpen_sInter` in the root
  namespace instead. -/
  protected isOpen_sInter : âˆ€ S : Set (Set Î±), (âˆ€ s âˆˆ S, IsOpen s) â†’ IsOpen (â‹‚â‚€ S)

variable {Î¹ : Sort*} {Îº : Î¹ â†’ Sort*} {Î± Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²]

instance DiscreteTopology.toAlexandrovDiscrete [DiscreteTopology Î±] : AlexandrovDiscrete Î± where
  isOpen_sInter _ _ := isOpen_discrete _

instance Finite.toAlexandrovDiscrete [Finite Î±] : AlexandrovDiscrete Î± where
  isOpen_sInter S := (toFinite S).isOpen_sInter

section AlexandrovDiscrete
variable [AlexandrovDiscrete Î±] {S : Set (Set Î±)} {f : Î¹ â†’ Set Î±}

lemma isOpen_sInter : (âˆ€ s âˆˆ S, IsOpen s) â†’ IsOpen (â‹‚â‚€ S) := AlexandrovDiscrete.isOpen_sInter _

lemma isOpen_iInter (hf : âˆ€ i, IsOpen (f i)) : IsOpen (â‹‚ i, f i) :=
  isOpen_sInter <| forall_mem_range.2 hf

lemma isOpen_iInterâ‚‚ {f : âˆ€ i, Îº i â†’ Set Î±} (hf : âˆ€ i j, IsOpen (f i j)) :
    IsOpen (â‹‚ i, â‹‚ j, f i j) :=
  isOpen_iInter fun _ â†¦ isOpen_iInter <| hf _

lemma isClosed_sUnion (hS : âˆ€ s âˆˆ S, IsClosed s) : IsClosed (â‹ƒâ‚€ S) := by
  simp only [â† isOpen_compl_iff, compl_sUnion] at hS âŠ¢; exact isOpen_sInter <| forall_mem_image.2 hS

lemma isClosed_iUnion (hf : âˆ€ i, IsClosed (f i)) : IsClosed (â‹ƒ i, f i) :=
  isClosed_sUnion <| forall_mem_range.2 hf

lemma isClosed_iUnionâ‚‚ {f : âˆ€ i, Îº i â†’ Set Î±} (hf : âˆ€ i j, IsClosed (f i j)) :
    IsClosed (â‹ƒ i, â‹ƒ j, f i j) :=
  isClosed_iUnion fun _ â†¦ isClosed_iUnion <| hf _

lemma isClopen_sInter (hS : âˆ€ s âˆˆ S, IsClopen s) : IsClopen (â‹‚â‚€ S) :=
  âŸ¨isClosed_sInter fun s hs â†¦ (hS s hs).1, isOpen_sInter fun s hs â†¦ (hS s hs).2âŸ©

lemma isClopen_iInter (hf : âˆ€ i, IsClopen (f i)) : IsClopen (â‹‚ i, f i) :=
  âŸ¨isClosed_iInter fun i â†¦ (hf i).1, isOpen_iInter fun i â†¦ (hf i).2âŸ©

lemma isClopen_iInterâ‚‚ {f : âˆ€ i, Îº i â†’ Set Î±} (hf : âˆ€ i j, IsClopen (f i j)) :
    IsClopen (â‹‚ i, â‹‚ j, f i j) :=
  isClopen_iInter fun _ â†¦ isClopen_iInter <| hf _

lemma isClopen_sUnion (hS : âˆ€ s âˆˆ S, IsClopen s) : IsClopen (â‹ƒâ‚€ S) :=
  âŸ¨isClosed_sUnion fun s hs â†¦ (hS s hs).1, isOpen_sUnion fun s hs â†¦ (hS s hs).2âŸ©

lemma isClopen_iUnion (hf : âˆ€ i, IsClopen (f i)) : IsClopen (â‹ƒ i, f i) :=
  âŸ¨isClosed_iUnion fun i â†¦ (hf i).1, isOpen_iUnion fun i â†¦ (hf i).2âŸ©

lemma isClopen_iUnionâ‚‚ {f : âˆ€ i, Îº i â†’ Set Î±} (hf : âˆ€ i j, IsClopen (f i j)) :
    IsClopen (â‹ƒ i, â‹ƒ j, f i j) :=
  isClopen_iUnion fun _ â†¦ isClopen_iUnion <| hf _

lemma interior_iInter (f : Î¹ â†’ Set Î±) : interior (â‹‚ i, f i) = â‹‚ i, interior (f i) :=
  (interior_maximal (iInter_mono fun _ â†¦ interior_subset) <| isOpen_iInter fun _ â†¦
    isOpen_interior).antisymm' <| subset_iInter fun _ â†¦ interior_mono <| iInter_subset _ _

lemma interior_sInter (S : Set (Set Î±)) : interior (â‹‚â‚€ S) = â‹‚ s âˆˆ S, interior s := by
  simp_rw [sInter_eq_biInter, interior_iInter]

lemma closure_iUnion (f : Î¹ â†’ Set Î±) : closure (â‹ƒ i, f i) = â‹ƒ i, closure (f i) :=
  compl_injective <| by
    simpa only [â† interior_compl, compl_iUnion] using interior_iInter fun i â†¦ (f i)á¶œ

lemma closure_sUnion (S : Set (Set Î±)) : closure (â‹ƒâ‚€ S) = â‹ƒ s âˆˆ S, closure s := by
  simp_rw [sUnion_eq_biUnion, closure_iUnion]

end AlexandrovDiscrete

variable {s t : Set Î±} {a x y : Î±}

/-- The *exterior* of a set is the intersection of all its neighborhoods. In an Alexandrov-discrete
space, this is the smallest neighborhood of the set.

Note that this construction is unnamed in the literature. We choose the name in analogy to
`interior`. -/
def exterior (s : Set Î±) : Set Î± := (ð“Ë¢ s).ker

lemma exterior_singleton_eq_ker_nhds (a : Î±) : exterior {a} = (ð“ a).ker := by simp [exterior]

lemma exterior_def (s : Set Î±) : exterior s = â‹‚â‚€ {t : Set Î± | IsOpen t âˆ§ s âŠ† t} :=
  (hasBasis_nhdsSet _).ker.trans sInter_eq_biInter.symm

lemma mem_exterior : a âˆˆ exterior s â†” âˆ€ U, IsOpen U â†’ s âŠ† U â†’ a âˆˆ U := by simp [exterior_def]

lemma subset_exterior_iff : s âŠ† exterior t â†” âˆ€ U, IsOpen U â†’ t âŠ† U â†’ s âŠ† U := by
  simp [exterior_def]

lemma subset_exterior : s âŠ† exterior s := subset_exterior_iff.2 fun _ _ â†¦ id

lemma exterior_minimal (hâ‚ : s âŠ† t) (hâ‚‚ : IsOpen t) : exterior s âŠ† t := by
  rw [exterior_def]; exact sInter_subset_of_mem âŸ¨hâ‚‚, hâ‚âŸ©

lemma IsOpen.exterior_eq (h : IsOpen s) : exterior s = s :=
  (exterior_minimal Subset.rfl h).antisymm subset_exterior

lemma IsOpen.exterior_subset_iff (ht : IsOpen t) : exterior s âŠ† t â†” s âŠ† t :=
  âŸ¨subset_exterior.trans, fun h â†¦ exterior_minimal h htâŸ©

@[mono] lemma exterior_mono : Monotone (exterior : Set Î± â†’ Set Î±) :=
  fun _s _t h â†¦ ker_mono <| nhdsSet_mono h

@[simp] lemma exterior_empty : exterior (âˆ… : Set Î±) = âˆ… := isOpen_empty.exterior_eq
@[simp] lemma exterior_univ : exterior (univ : Set Î±) = univ := isOpen_univ.exterior_eq

@[simp] lemma exterior_eq_empty : exterior s = âˆ… â†” s = âˆ… :=
  âŸ¨eq_bot_mono subset_exterior, by rintro rfl; exact exterior_emptyâŸ©

variable [AlexandrovDiscrete Î±] [AlexandrovDiscrete Î²]

@[simp] lemma isOpen_exterior : IsOpen (exterior s) := by
  rw [exterior_def]; exact isOpen_sInter fun _ â†¦ And.left

lemma exterior_mem_nhdsSet : exterior s âˆˆ ð“Ë¢ s := isOpen_exterior.mem_nhdsSet.2 subset_exterior

@[simp] lemma exterior_eq_iff_isOpen : exterior s = s â†” IsOpen s :=
  âŸ¨fun h â†¦ h â–¸ isOpen_exterior, IsOpen.exterior_eqâŸ©

@[simp] lemma exterior_subset_iff_isOpen : exterior s âŠ† s â†” IsOpen s := by
  simp only [exterior_eq_iff_isOpen.symm, Subset.antisymm_iff, subset_exterior, and_true]

lemma exterior_subset_iff : exterior s âŠ† t â†” âˆƒ U, IsOpen U âˆ§ s âŠ† U âˆ§ U âŠ† t :=
  âŸ¨fun h â†¦ âŸ¨exterior s, isOpen_exterior, subset_exterior, hâŸ©,
    fun âŸ¨_U, hU, hsU, hUtâŸ© â†¦ (exterior_minimal hsU hU).trans hUtâŸ©

lemma exterior_subset_iff_mem_nhdsSet : exterior s âŠ† t â†” t âˆˆ ð“Ë¢ s :=
  exterior_subset_iff.trans mem_nhdsSet_iff_exists.symm

lemma exterior_singleton_subset_iff_mem_nhds : exterior {a} âŠ† t â†” t âˆˆ ð“ a := by
  simp [exterior_subset_iff_mem_nhdsSet]

lemma IsOpen.exterior_subset (ht : IsOpen t) : exterior s âŠ† t â†” s âŠ† t :=
  âŸ¨subset_exterior.trans, fun h â†¦ exterior_minimal h htâŸ©

lemma gc_exterior_interior : GaloisConnection (exterior : Set Î± â†’ Set Î±) interior :=
  fun s t â†¦ by simp [exterior_subset_iff, subset_interior_iff]

@[simp] lemma exterior_exterior (s : Set Î±) : exterior (exterior s) = exterior s :=
  isOpen_exterior.exterior_eq

@[simp] lemma exterior_union (s t : Set Î±) : exterior (s âˆª t) = exterior s âˆª exterior t :=
  gc_exterior_interior.l_sup

@[simp] lemma nhdsSet_exterior (s : Set Î±) : ð“Ë¢ (exterior s) = ð“Ë¢ s := by
  ext t; simp_rw [â† exterior_subset_iff_mem_nhdsSet, exterior_exterior]

@[simp] lemma principal_exterior (s : Set Î±) : ð“Ÿ (exterior s) = ð“Ë¢ s := by
  rw [â† nhdsSet_exterior, isOpen_exterior.nhdsSet_eq]

@[simp] lemma exterior_subset_exterior : exterior s âŠ† exterior t â†” ð“Ë¢ s â‰¤ ð“Ë¢ t := by
  refine âŸ¨?_, fun h â†¦ ker_mono hâŸ©
  simp_rw [le_def, â† exterior_subset_iff_mem_nhdsSet]
  exact fun h u â†¦ h.trans

lemma specializes_iff_exterior_subset : x â¤³ y â†” exterior {x} âŠ† exterior {y} := by
  simp [Specializes]

lemma isOpen_iff_forall_specializes : IsOpen s â†” âˆ€ x y, x â¤³ y â†’ y âˆˆ s â†’ x âˆˆ s := by
  refine âŸ¨fun hs x y hxy â†¦ hxy.mem_open hs, fun hs â†¦ ?_âŸ©
  simp_rw [specializes_iff_exterior_subset] at hs
  simp_rw [isOpen_iff_mem_nhds, mem_nhds_iff]
  rintro a ha
  refine âŸ¨_, fun b hb â†¦ hs _ _ ?_ ha, isOpen_exterior, subset_exterior <| mem_singleton _âŸ©
  rwa [isOpen_exterior.exterior_subset, singleton_subset_iff]

lemma Set.Finite.isCompact_exterior (hs : s.Finite) : IsCompact (exterior s) := by
  classical
  refine isCompact_of_finite_subcover fun f hf hsf â†¦ ?_
  choose g hg using fun a (ha : a âˆˆ exterior s) â†¦ mem_iUnion.1 (hsf ha)
  refine âŸ¨hs.toFinset.attach.image fun a â†¦
    g a.1 <| subset_exterior <| (Finite.mem_toFinset _).1 a.2,
    (isOpen_iUnion fun i â†¦ isOpen_iUnion ?_).exterior_subset.2 ?_âŸ©
  Â· exact fun _ â†¦ hf _
  refine fun a ha â†¦ mem_iUnionâ‚‚.2 âŸ¨_, ?_, hg _ <| subset_exterior haâŸ©
  simp only [Finset.mem_image, Finset.mem_attach, true_and, Subtype.exists, Finite.mem_toFinset]
  exact âŸ¨a, ha, rflâŸ©

lemma Inducing.alexandrovDiscrete {f : Î² â†’ Î±} (h : Inducing f) : AlexandrovDiscrete Î² where
  isOpen_sInter S hS := by
    simp_rw [h.isOpen_iff] at hS âŠ¢
    choose U hU htU using hS
    refine âŸ¨_, isOpen_iInterâ‚‚ hU, ?_âŸ©
    simp_rw [preimage_iInter, htU, sInter_eq_biInter]

lemma alexandrovDiscrete_coinduced {Î² : Type*} {f : Î± â†’ Î²} :
    @AlexandrovDiscrete Î² (coinduced f â€¹_â€º) :=
  @AlexandrovDiscrete.mk Î² (coinduced f â€¹_â€º) fun S hS â†¦ by
    rw [isOpen_coinduced, preimage_sInter]; exact isOpen_iInterâ‚‚ hS

lemma AlexandrovDiscrete.sup {tâ‚ tâ‚‚ : TopologicalSpace Î±} (_ : @AlexandrovDiscrete Î± tâ‚)
    (_ : @AlexandrovDiscrete Î± tâ‚‚) :
    @AlexandrovDiscrete Î± (tâ‚ âŠ” tâ‚‚) :=
  @AlexandrovDiscrete.mk Î± (tâ‚ âŠ” tâ‚‚) fun _S hS â†¦
    âŸ¨@isOpen_sInter _ tâ‚ _ _ fun _s hs â†¦ (hS _ hs).1, isOpen_sInter fun _s hs â†¦ (hS _ hs).2âŸ©

lemma alexandrovDiscrete_iSup {t : Î¹ â†’ TopologicalSpace Î±} (_ : âˆ€ i, @AlexandrovDiscrete Î± (t i)) :
    @AlexandrovDiscrete Î± (â¨† i, t i) :=
  @AlexandrovDiscrete.mk Î± (â¨† i, t i)
    fun _S hS â†¦ isOpen_iSup_iff.2
      fun i â†¦ @isOpen_sInter _ (t i) _ _
        fun _s hs â†¦ isOpen_iSup_iff.1 (hS _ hs) _

instance AlexandrovDiscrete.toFirstCountable : FirstCountableTopology Î± where
  nhds_generated_countable a := âŸ¨{exterior {a}}, countable_singleton _, by simpâŸ©

instance AlexandrovDiscrete.toLocallyCompactSpace : LocallyCompactSpace Î± where
  local_compact_nhds a _U hU := âŸ¨exterior {a},
    isOpen_exterior.mem_nhds <| subset_exterior <| mem_singleton _,
      exterior_singleton_subset_iff_mem_nhds.2 hU, (finite_singleton _).isCompact_exteriorâŸ©

instance Subtype.instAlexandrovDiscrete {p : Î± â†’ Prop} : AlexandrovDiscrete {a // p a} :=
  inducing_subtype_val.alexandrovDiscrete

instance Quotient.instAlexandrovDiscrete {s : Setoid Î±} : AlexandrovDiscrete (Quotient s) :=
  alexandrovDiscrete_coinduced

instance Sum.instAlexandrovDiscrete : AlexandrovDiscrete (Î± âŠ• Î²) :=
  alexandrovDiscrete_coinduced.sup alexandrovDiscrete_coinduced

instance Sigma.instAlexandrovDiscrete {Î¹ : Type*} {Ï€ : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (Ï€ i)]
    [âˆ€ i, AlexandrovDiscrete (Ï€ i)] : AlexandrovDiscrete (Î£ i, Ï€ i) :=
  alexandrovDiscrete_iSup fun _ â†¦ alexandrovDiscrete_coinduced
