/-
Copyright (c) 2023 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Data.Set.Image
import Mathlib.Topology.Bases
import Mathlib.Topology.Inseparable
import Mathlib.Topology.Compactness.NhdsKer

/-!
# Alexandrov-discrete topological spaces

This file defines Alexandrov-discrete spaces, aka finitely generated spaces.

A space is Alexandrov-discrete if the (arbitrary) intersection of open sets is open. As such,
the intersection of all neighborhoods of a set is a neighborhood itself. Hence every set has a
minimal neighborhood, which we call the *neighborhoods kernel* of the set.

## Main declarations

* `AlexandrovDiscrete`: Prop-valued typeclass for a topological space to be Alexandrov-discrete

## TODO

Finite product of Alexandrov-discrete spaces is Alexandrov-discrete.

## Tags

Alexandroff, discrete, finitely generated, fg space
-/

open Filter Set TopologicalSpace Topology

/-- A topological space is **Alexandrov-discrete** or **finitely generated** if the intersection of
a family of open sets is open. -/
class AlexandrovDiscrete (Î± : Type*) [TopologicalSpace Î±] : Prop where
  /-- The intersection of a family of open sets is an open set. Use `isOpen_sInter` in the root
  namespace instead. -/
  protected isOpen_sInter : âˆ€ S : Set (Set Î±), (âˆ€ s âˆˆ S, IsOpen s) â†’ IsOpen (â‹‚â‚€ S)

variable {Î¹ : Sort*} {Îº : Î¹ â†’ Sort*} {Î± Î² : Type*}
section
variable [TopologicalSpace Î±] [TopologicalSpace Î²]

instance DiscreteTopology.toAlexandrovDiscrete [DiscreteTopology Î±] : AlexandrovDiscrete Î± where
  isOpen_sInter _ _ := isOpen_discrete _

instance Finite.toAlexandrovDiscrete [Finite Î±] : AlexandrovDiscrete Î± where
  isOpen_sInter S := (toFinite S).isOpen_sInter

section AlexandrovDiscrete
variable [AlexandrovDiscrete Î±] {S : Set (Set Î±)} {f : Î¹ â†’ Set Î±}

lemma isOpen_sInter : (âˆ€ s âˆˆ S, IsOpen s) â†’ IsOpen (â‹‚â‚€ S) := AlexandrovDiscrete.isOpen_sInter _

lemma isOpen_iInter (hf : âˆ€ i, IsOpen (f i)) : IsOpen (â‹‚ i, f i) :=
  isOpen_sInter <| forall_mem_range.2 hf

lemma isOpen_iInterâ‚‚ {f : âˆ€ i, Îº i â†’ Set Î±} (hf : âˆ€ i j, IsOpen (f i j)) :
    IsOpen (â‹‚ i, â‹‚ j, f i j) :=
  isOpen_iInter fun _ â†¦ isOpen_iInter <| hf _

lemma isClosed_sUnion (hS : âˆ€ s âˆˆ S, IsClosed s) : IsClosed (â‹ƒâ‚€ S) := by
  simp only [â† isOpen_compl_iff, compl_sUnion] at hS âŠ¢; exact isOpen_sInter <| forall_mem_image.2 hS

lemma isClosed_iUnion (hf : âˆ€ i, IsClosed (f i)) : IsClosed (â‹ƒ i, f i) :=
  isClosed_sUnion <| forall_mem_range.2 hf

lemma isClosed_iUnionâ‚‚ {f : âˆ€ i, Îº i â†’ Set Î±} (hf : âˆ€ i j, IsClosed (f i j)) :
    IsClosed (â‹ƒ i, â‹ƒ j, f i j) :=
  isClosed_iUnion fun _ â†¦ isClosed_iUnion <| hf _

lemma isClopen_sInter (hS : âˆ€ s âˆˆ S, IsClopen s) : IsClopen (â‹‚â‚€ S) :=
  âŸ¨isClosed_sInter fun s hs â†¦ (hS s hs).1, isOpen_sInter fun s hs â†¦ (hS s hs).2âŸ©

lemma isClopen_iInter (hf : âˆ€ i, IsClopen (f i)) : IsClopen (â‹‚ i, f i) :=
  âŸ¨isClosed_iInter fun i â†¦ (hf i).1, isOpen_iInter fun i â†¦ (hf i).2âŸ©

lemma isClopen_iInterâ‚‚ {f : âˆ€ i, Îº i â†’ Set Î±} (hf : âˆ€ i j, IsClopen (f i j)) :
    IsClopen (â‹‚ i, â‹‚ j, f i j) :=
  isClopen_iInter fun _ â†¦ isClopen_iInter <| hf _

lemma isClopen_sUnion (hS : âˆ€ s âˆˆ S, IsClopen s) : IsClopen (â‹ƒâ‚€ S) :=
  âŸ¨isClosed_sUnion fun s hs â†¦ (hS s hs).1, isOpen_sUnion fun s hs â†¦ (hS s hs).2âŸ©

lemma isClopen_iUnion (hf : âˆ€ i, IsClopen (f i)) : IsClopen (â‹ƒ i, f i) :=
  âŸ¨isClosed_iUnion fun i â†¦ (hf i).1, isOpen_iUnion fun i â†¦ (hf i).2âŸ©

lemma isClopen_iUnionâ‚‚ {f : âˆ€ i, Îº i â†’ Set Î±} (hf : âˆ€ i j, IsClopen (f i j)) :
    IsClopen (â‹ƒ i, â‹ƒ j, f i j) :=
  isClopen_iUnion fun _ â†¦ isClopen_iUnion <| hf _

lemma interior_iInter (f : Î¹ â†’ Set Î±) : interior (â‹‚ i, f i) = â‹‚ i, interior (f i) :=
  (interior_maximal (iInter_mono fun _ â†¦ interior_subset) <| isOpen_iInter fun _ â†¦
    isOpen_interior).antisymm' <| subset_iInter fun _ â†¦ interior_mono <| iInter_subset _ _

lemma interior_sInter (S : Set (Set Î±)) : interior (â‹‚â‚€ S) = â‹‚ s âˆˆ S, interior s := by
  simp_rw [sInter_eq_biInter, interior_iInter]

lemma closure_iUnion (f : Î¹ â†’ Set Î±) : closure (â‹ƒ i, f i) = â‹ƒ i, closure (f i) :=
  compl_injective <| by
    simpa only [â† interior_compl, compl_iUnion] using interior_iInter fun i â†¦ (f i)á¶œ

lemma closure_sUnion (S : Set (Set Î±)) : closure (â‹ƒâ‚€ S) = â‹ƒ s âˆˆ S, closure s := by
  simp_rw [sUnion_eq_biUnion, closure_iUnion]

end AlexandrovDiscrete

lemma Topology.IsInducing.alexandrovDiscrete [AlexandrovDiscrete Î±] {f : Î² â†’ Î±} (h : IsInducing f) :
    AlexandrovDiscrete Î² where
  isOpen_sInter S hS := by
    simp_rw [h.isOpen_iff] at hS âŠ¢
    choose U hU htU using hS
    refine âŸ¨_, isOpen_iInterâ‚‚ hU, ?_âŸ©
    simp_rw [preimage_iInter, htU, sInter_eq_biInter]

@[deprecated (since := "2024-10-28")]
alias Inducing.alexandrovDiscrete := IsInducing.alexandrovDiscrete

end

lemma AlexandrovDiscrete.sup {tâ‚ tâ‚‚ : TopologicalSpace Î±} (_ : @AlexandrovDiscrete Î± tâ‚)
    (_ : @AlexandrovDiscrete Î± tâ‚‚) :
    @AlexandrovDiscrete Î± (tâ‚ âŠ” tâ‚‚) :=
  @AlexandrovDiscrete.mk Î± (tâ‚ âŠ” tâ‚‚) fun _S hS â†¦
    âŸ¨@isOpen_sInter _ tâ‚ _ _ fun _s hs â†¦ (hS _ hs).1, isOpen_sInter fun _s hs â†¦ (hS _ hs).2âŸ©

lemma alexandrovDiscrete_iSup {t : Î¹ â†’ TopologicalSpace Î±} (_ : âˆ€ i, @AlexandrovDiscrete Î± (t i)) :
    @AlexandrovDiscrete Î± (â¨† i, t i) :=
  @AlexandrovDiscrete.mk Î± (â¨† i, t i)
    fun _S hS â†¦ isOpen_iSup_iff.2
      fun i â†¦ @isOpen_sInter _ (t i) _ _
        fun _s hs â†¦ isOpen_iSup_iff.1 (hS _ hs) _

section
variable [TopologicalSpace Î±] [TopologicalSpace Î²] [AlexandrovDiscrete Î±] [AlexandrovDiscrete Î²]
  {s t : Set Î±} {a : Î±}

@[simp] lemma isOpen_nhdsKer : IsOpen (nhdsKer s) := by
  rw [nhdsKer_def]; exact isOpen_sInter fun _ â†¦ And.left

@[deprecated (since := "2025-07-09")] alias isOpen_exterior := isOpen_nhdsKer

lemma nhdsKer_mem_nhdsSet : nhdsKer s âˆˆ ð“Ë¢ s := isOpen_nhdsKer.mem_nhdsSet.2 subset_nhdsKer

@[deprecated (since := "2025-07-09")] alias exterior_mem_nhdsSet := nhdsKer_mem_nhdsSet

@[simp] lemma nhdsKer_eq_iff_isOpen : nhdsKer s = s â†” IsOpen s :=
  âŸ¨fun h â†¦ h â–¸ isOpen_nhdsKer, IsOpen.nhdsKer_eqâŸ©

@[deprecated (since := "2025-07-09")] alias exterior_eq_iff_isOpen := nhdsKer_eq_iff_isOpen

@[simp] lemma nhdsKer_subset_iff_isOpen : nhdsKer s âŠ† s â†” IsOpen s := by
  simp only [nhdsKer_eq_iff_isOpen.symm, Subset.antisymm_iff, subset_nhdsKer, and_true]

@[deprecated (since := "2025-07-09")] alias exterior_subset_iff_isOpen := nhdsKer_subset_iff_isOpen

lemma nhdsKer_subset_iff : nhdsKer s âŠ† t â†” âˆƒ U, IsOpen U âˆ§ s âŠ† U âˆ§ U âŠ† t :=
  âŸ¨fun h â†¦ âŸ¨nhdsKer s, isOpen_nhdsKer, subset_nhdsKer, hâŸ©,
    fun âŸ¨_U, hU, hsU, hUtâŸ© â†¦ (nhdsKer_minimal hsU hU).trans hUtâŸ©

@[deprecated (since := "2025-07-09")] alias exterior_subset_iff := nhdsKer_subset_iff

lemma nhdsKer_subset_iff_mem_nhdsSet : nhdsKer s âŠ† t â†” t âˆˆ ð“Ë¢ s :=
  nhdsKer_subset_iff.trans mem_nhdsSet_iff_exists.symm

@[deprecated (since := "2025-07-09")]
alias exterior_subset_iff_mem_nhdsSet := nhdsKer_subset_iff_mem_nhdsSet

lemma nhdsKer_singleton_subset_iff_mem_nhds : nhdsKer {a} âŠ† t â†” t âˆˆ ð“ a := by
  simp [nhdsKer_subset_iff_mem_nhdsSet]

@[deprecated (since := "2025-07-09")]
alias exterior_singleton_subset_iff_mem_nhds := nhdsKer_singleton_subset_iff_mem_nhds

lemma gc_nhdsKer_interior : GaloisConnection (nhdsKer : Set Î± â†’ Set Î±) interior :=
  fun s t â†¦ by simp [nhdsKer_subset_iff, subset_interior_iff]

@[deprecated (since := "2025-07-09")] alias gc_exterior_interior := gc_nhdsKer_interior

@[simp] lemma principal_nhdsKer (s : Set Î±) : ð“Ÿ (nhdsKer s) = ð“Ë¢ s := by
  rw [â† nhdsSet_nhdsKer, isOpen_nhdsKer.nhdsSet_eq]

@[deprecated (since := "2025-07-09")] alias principal_exterior := principal_nhdsKer

lemma isOpen_iff_forall_specializes : IsOpen s â†” âˆ€ x y, x â¤³ y â†’ y âˆˆ s â†’ x âˆˆ s := by
  simp only [â† nhdsKer_subset_iff_isOpen, Set.subset_def, mem_nhdsKer_iff_specializes, exists_imp,
    and_imp, @forall_swap (_ â¤³ _)]

lemma alexandrovDiscrete_coinduced {Î² : Type*} {f : Î± â†’ Î²} :
    @AlexandrovDiscrete Î² (coinduced f â€¹_â€º) :=
  @AlexandrovDiscrete.mk Î² (coinduced f â€¹_â€º) fun S hS â†¦ by
    rw [isOpen_coinduced, preimage_sInter]; exact isOpen_iInterâ‚‚ hS

instance AlexandrovDiscrete.toFirstCountable : FirstCountableTopology Î± where
  nhds_generated_countable a := âŸ¨{nhdsKer {a}}, countable_singleton _, by simpâŸ©

instance AlexandrovDiscrete.toLocallyCompactSpace : LocallyCompactSpace Î± where
  local_compact_nhds a _U hU := âŸ¨nhdsKer {a},
    isOpen_nhdsKer.mem_nhds <| subset_nhdsKer <| mem_singleton _,
      nhdsKer_singleton_subset_iff_mem_nhds.2 hU, isCompact_singleton.nhdsKerâŸ©

instance Subtype.instAlexandrovDiscrete {p : Î± â†’ Prop} : AlexandrovDiscrete {a // p a} :=
  IsInducing.subtypeVal.alexandrovDiscrete

instance Quotient.instAlexandrovDiscrete {s : Setoid Î±} : AlexandrovDiscrete (Quotient s) :=
  alexandrovDiscrete_coinduced

instance Sum.instAlexandrovDiscrete : AlexandrovDiscrete (Î± âŠ• Î²) :=
  alexandrovDiscrete_coinduced.sup alexandrovDiscrete_coinduced

instance Sigma.instAlexandrovDiscrete {Î¹ : Type*} {Ï€ : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (Ï€ i)]
    [âˆ€ i, AlexandrovDiscrete (Ï€ i)] : AlexandrovDiscrete (Î£ i, Ï€ i) :=
  alexandrovDiscrete_iSup fun _ â†¦ alexandrovDiscrete_coinduced

end
