/-
Copyright (c) 2023 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Topology.Bases
import Mathlib.Topology.Inseparable
import Mathlib.Topology.SubsetProperties

/-!
# Alexandrov-discrete topological spaces

This file defines Alexandrov-discrete spaces, aka finitely generated spaces.

A space is Alexandrov-discrete if the (arbitrary) intersection of open sets is open.

## TODO

Rest of the API

## Tags

Alexandroff, discrete, finitely generated, fg space
-/

open Filter Set TopologicalSpace
open scoped Topology

/-- A topological space is **Alexandrov-discrete** or **finitely generated** if the intersection of
a family of open sets is open. -/
class AlexandrovDiscrete (Î± : Type*) [TopologicalSpace Î±] : Prop where
  /-- The intersection of a family of open sets is an open set. Use `isOpen_sInter` in the root
  namespace instead. -/
  protected isOpen_sInter : âˆ€ s : Set (Set Î±), (âˆ€ t âˆˆ s, IsOpen t) â†’ IsOpen (â‹‚â‚€ s)

variable {Î¹ : Sort*} {Î± : Type*} [TopologicalSpace Î±]

section AlexandrovDiscrete
variable [AlexandrovDiscrete Î±] {S : Set (Set Î±)} {f : Î¹ â†’ Set Î±}

lemma isOpen_sInter : (âˆ€ s âˆˆ S, IsOpen s) â†’ IsOpen (â‹‚â‚€ S) := AlexandrovDiscrete.isOpen_sInter _

lemma isOpen_iInter (hf : âˆ€ i, IsOpen (f i)) : IsOpen (â‹‚ i, f i) :=
isOpen_sInter $ forall_range_iff.2 hf

lemma isClosed_sUnion (hS : âˆ€ s âˆˆ S, IsClosed s) : IsClosed (â‹ƒâ‚€ S) := by
  simp only [â†isOpen_compl_iff, compl_sUnion] at hS âŠ¢; exact isOpen_sInter $ ball_image_iff.2 hS

lemma isClosed_iUnion (hf : âˆ€ i, IsClosed (f i)) : IsClosed (â‹ƒ i, f i) :=
isClosed_sUnion $ forall_range_iff.2 hf

lemma isClopen_sUnion (hS : âˆ€ s âˆˆ S, IsClopen s) : IsClopen (â‹ƒâ‚€ S) :=
âŸ¨isOpen_sUnion Î» s hs â†¦ (hS s hs).1, isClosed_sUnion Î» s hs â†¦ (hS s hs).2âŸ©

lemma isClopen_iUnion (hf : âˆ€ i, IsClopen (f i)) : IsClopen (â‹ƒ i, f i) :=
âŸ¨isOpen_iUnion Î» i â†¦ (hf i).1, isClosed_iUnion Î» i â†¦ (hf i).2âŸ©

lemma interior_iInter (f : Î¹ â†’ Set Î±) : interior (â‹‚ i, f i) = â‹‚ i, interior (f i) :=
(interior_maximal (iInter_mono Î» _ â†¦ interior_subset) $ isOpen_iInter Î» _ â†¦
  isOpen_interior).antisymm' $ subset_iInter Î» _ â†¦ interior_mono $ iInter_subset _ _

lemma interior_sInter (S : Set (Set Î±)) : interior (â‹‚â‚€ S) = â‹‚ s âˆˆ S, interior s := by
  simp_rw [sInter_eq_biInter, interior_iInter]

lemma closure_iUnion (f : Î¹ â†’ Set Î±) : closure (â‹ƒ i, f i) = â‹ƒ i, closure (f i) :=
compl_injective $ by simpa only [â†interior_compl, compl_iUnion] using interior_iInter Î» i â†¦ (f i)á¶œ

lemma closure_sUnion (S : Set (Set Î±)) : closure (â‹ƒâ‚€ S) = â‹ƒ s âˆˆ S, closure s := by
  simp_rw [sUnion_eq_biUnion, closure_iUnion]

end AlexandrovDiscrete

variable {s t : Set Î±} {x y : Î±}

/-- The *exterior* of a set is the intersection of all its neighborhoods. In an Alexandrov-discrete
space, this is the smallest neighborhood of the set. -/
def exterior (s : Set Î±) : Set Î± := (ğ“Ë¢ s).ker

lemma exterior_singleton_eq_ker_nhds (a : Î±) : exterior {a} = (ğ“ a).ker := by simp [exterior]

lemma exterior_def (s : Set Î±) : exterior s = â‹‚â‚€ {t : Set Î± | IsOpen t âˆ§ s âŠ† t} := by
  ext a
  simp only [exterior, mem_ker, mem_sInter, mem_setOf_eq, and_imp, mem_nhdsSet_iff_forall]
  refine' âŸ¨Î» hs t ht hts â†¦ hs _ Î» b hb â†¦ ht.mem_nhds $ hts hb, Î» hs t ht â†¦ interior_subset $
    hs (interior t) isOpen_interior _âŸ©
  simpa only [â†mem_interior_iff_mem_nhds] using ht

lemma subset_exterior_iff : s âŠ† exterior t â†” âˆ€ U, IsOpen U â†’ t âŠ† U â†’ s âŠ† U := by
  simp [exterior_def]

lemma interior_subset_iff : interior s âŠ† t â†” âˆ€ U, IsOpen U â†’ U âŠ† s â†’ U âŠ† t := by
  simp [interior]

lemma subset_exterior : s âŠ† exterior s := subset_exterior_iff.2 $ Î» _ _ â†¦ id

lemma exterior_minimal (hâ‚ : s âŠ† t) (hâ‚‚ : IsOpen t) : exterior s âŠ† t := by
  rw [exterior_def]; exact sInter_subset_of_mem âŸ¨hâ‚‚, hâ‚âŸ©

lemma IsOpen.exterior_eq (h : IsOpen s) : exterior s = s :=
(exterior_minimal Subset.rfl h).antisymm subset_exterior

lemma IsOpen.exterior_subset_iff (ht : IsOpen t) : exterior s âŠ† t â†” s âŠ† t :=
âŸ¨subset_exterior.trans, Î» h â†¦ exterior_minimal h htâŸ©

@[mono] lemma exterior_mono : Monotone (exterior : Set Î± â†’ Set Î±) :=
Î» _s _t h â†¦ ker_mono $ nhdsSet_mono h

@[simp] lemma exterior_empty : exterior (âˆ… : Set Î±) = âˆ… := isOpen_empty.exterior_eq
@[simp] lemma exterior_univ : exterior (univ : Set Î±) = univ := isOpen_univ.exterior_eq

@[simp] lemma exterior_eq_empty : exterior s = âˆ… â†” s = âˆ… :=
âŸ¨eq_bot_mono subset_exterior, by rintro rfl; exact exterior_emptyâŸ©

variable [AlexandrovDiscrete Î±]

@[simp] lemma isOpen_exterior : IsOpen (exterior s) := by
  rw [exterior_def]; exact isOpen_sInter $ Î» _ â†¦ And.left

@[simp] lemma exterior_eq_iff_isOpen : exterior s = s â†” IsOpen s :=
  âŸ¨Î» h â†¦ h â–¸ isOpen_exterior, IsOpen.exterior_eqâŸ©

@[simp] lemma exterior_subset_iff_isOpen : exterior s âŠ† s â†” IsOpen s := by
  simp only [exterior_eq_iff_isOpen.symm, Subset.antisymm_iff, subset_exterior, and_true]

lemma exterior_subset : exterior s âŠ† t â†” âˆƒ U, IsOpen U âˆ§ s âŠ† U âˆ§ U âŠ† t :=
âŸ¨Î» h â†¦ âŸ¨exterior s, isOpen_exterior, subset_exterior, hâŸ©,
  Î» âŸ¨_U, hU, hsU, hUtâŸ© â†¦ (exterior_minimal hsU hU).trans hUtâŸ©

lemma exterior_subset_iff_mem_nhds : exterior s âŠ† t â†” t âˆˆ ğ“Ë¢ s :=
exterior_subset.trans mem_nhdsSet_iff_exists.symm

lemma IsOpen.exterior_subset (ht : IsOpen t) : exterior s âŠ† t â†” s âŠ† t :=
âŸ¨subset_exterior.trans, Î» h â†¦ exterior_minimal h htâŸ©

lemma gc_exterior_interior : GaloisConnection (exterior : Set Î± â†’ Set Î±) interior :=
Î» s t â†¦ by simp [exterior_subset, subset_interior_iff]

@[simp] lemma exterior_exterior (s : Set Î±) : exterior (exterior s) = exterior s :=
isOpen_exterior.exterior_eq

@[simp] lemma exterior_union (s t : Set Î±) : exterior (s âˆª t) = exterior s âˆª exterior t :=
gc_exterior_interior.l_sup

@[simp] lemma nhdsSet_exterior (s : Set Î±) : ğ“Ë¢ (exterior s) = ğ“Ÿ (exterior s) :=
isOpen_exterior.nhdsSet_eq

@[simp] lemma exterior_subset_exterior : exterior s âŠ† exterior t â†” ğ“Ë¢ s â‰¤ ğ“Ë¢ t := by
  refine âŸ¨?_, Î» h â†¦ ker_mono hâŸ©
  simp_rw [le_def, â†exterior_subset_iff_mem_nhds]
  exact Î» h u â†¦ h.trans

lemma specializes_iff_exterior_subset : x â¤³ y â†” exterior {x} âŠ† exterior {y} := by
  simp [Specializes]

lemma isOpen_iff_forall_specializes : IsOpen s â†” âˆ€ x y, x â¤³ y â†’ y âˆˆ s â†’ x âˆˆ s := by
  refine' âŸ¨Î» hs x y hxy â†¦ hxy.mem_open hs, Î» hs â†¦ _âŸ©
  simp_rw [specializes_iff_exterior_subset] at hs
  simp_rw [isOpen_iff_mem_nhds, mem_nhds_iff]
  rintro a ha
  refine âŸ¨_, Î» b hb â†¦ hs _ _ ?_ ha, isOpen_exterior, subset_exterior $ mem_singleton _âŸ©
  rwa [isOpen_exterior.exterior_subset, singleton_subset_iff]
