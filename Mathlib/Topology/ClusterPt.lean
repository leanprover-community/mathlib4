/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Jeremy Avigad
-/
import Mathlib.Topology.Neighborhoods

/-!
# Lemmas on cluster and accumulation points

In this file we prove various lemmas on [cluster points](https://en.wikipedia.org/wiki/Limit_point)
(also known as limit points and accumulation points) of a filter and of a sequence.

A filter `F` on `X` has `x` as a cluster point if `ClusterPt x F : ğ“ x âŠ“ F â‰  âŠ¥`. A map `f : Î± â†’ X`
clusters at `x` along `F : Filter Î±` if `MapClusterPt x F f : ClusterPt x (map f F)`.
In particular the notion of cluster point of a sequence `u` is `MapClusterPt x atTop u`.
-/

open Set Filter Topology

universe u v w

variable {X : Type u} [TopologicalSpace X] {Y : Type v} {Î¹ : Sort w} {Î± Î² : Type*}
  {x : X} {s sâ‚ sâ‚‚ t : Set X}

theorem clusterPt_sup {F G : Filter X} : ClusterPt x (F âŠ” G) â†” ClusterPt x F âˆ¨ ClusterPt x G := by
  simp only [ClusterPt, inf_sup_left, sup_neBot]

theorem ClusterPt.neBot {F : Filter X} (h : ClusterPt x F) : NeBot (ğ“ x âŠ“ F) :=
  h

theorem Filter.HasBasis.clusterPt_iff {Î¹X Î¹F} {pX : Î¹X â†’ Prop} {sX : Î¹X â†’ Set X} {pF : Î¹F â†’ Prop}
    {sF : Î¹F â†’ Set X} {F : Filter X} (hX : (ğ“ x).HasBasis pX sX) (hF : F.HasBasis pF sF) :
    ClusterPt x F â†” âˆ€ â¦ƒiâ¦„, pX i â†’ âˆ€ â¦ƒjâ¦„, pF j â†’ (sX i âˆ© sF j).Nonempty :=
  hX.inf_basis_neBot_iff hF

theorem Filter.HasBasis.clusterPt_iff_frequently {Î¹} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set X} {F : Filter X}
    (hx : (ğ“ x).HasBasis p s) : ClusterPt x F â†” âˆ€ i, p i â†’ âˆƒá¶  x in F, x âˆˆ s i := by
  simp only [hx.clusterPt_iff F.basis_sets, Filter.frequently_iff, inter_comm (s _),
    Set.Nonempty, id, mem_inter_iff]

theorem clusterPt_iff_frequently {F : Filter X} : ClusterPt x F â†” âˆ€ s âˆˆ ğ“ x, âˆƒá¶  y in F, y âˆˆ s :=
  (ğ“ x).basis_sets.clusterPt_iff_frequently

theorem ClusterPt.frequently {F : Filter X} {p : X â†’ Prop} (hx : ClusterPt x F)
    (hp : âˆ€á¶  y in ğ“ x, p y) : âˆƒá¶  y in F, p y :=
  clusterPt_iff_frequently.mp hx {y | p y} hp

theorem Filter.HasBasis.clusterPt_iff_frequently' {Î¹} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set X} {F : Filter X}
    (hx : F.HasBasis p s) : ClusterPt x F â†” âˆ€ i, p i â†’ âˆƒá¶  x in ğ“ x, x âˆˆ s i := by
  simp only [(ğ“ x).basis_sets.clusterPt_iff hx, Filter.frequently_iff]
  exact âŸ¨fun h a b c d â†¦ h d b, fun h a b c d â†¦ h c d bâŸ©

theorem clusterPt_iff_frequently' {F : Filter X} : ClusterPt x F â†” âˆ€ s âˆˆ F, âˆƒá¶  y in ğ“ x, y âˆˆ s :=
  F.basis_sets.clusterPt_iff_frequently'

theorem ClusterPt.frequently' {F : Filter X} {p : X â†’ Prop} (hx : ClusterPt x F)
    (hp : âˆ€á¶  y in F, p y) : âˆƒá¶  y in ğ“ x, p y :=
  clusterPt_iff_frequently'.mp hx {y | p y} hp

theorem clusterPt_iff_nonempty {F : Filter X} :
    ClusterPt x F â†” âˆ€ â¦ƒU : Set Xâ¦„, U âˆˆ ğ“ x â†’ âˆ€ â¦ƒVâ¦„, V âˆˆ F â†’ (U âˆ© V).Nonempty :=
  inf_neBot_iff

@[deprecated (since := "2025-03-16")]
alias clusterPt_iff := clusterPt_iff_nonempty

theorem clusterPt_iff_not_disjoint {F : Filter X} :
    ClusterPt x F â†” Â¬Disjoint (ğ“ x) F := by
  rw [disjoint_iff, ClusterPt, neBot_iff]

protected theorem Filter.HasBasis.clusterPt_iff_forall_mem_closure {Î¹} {p : Î¹ â†’ Prop}
    {s : Î¹ â†’ Set X} {F : Filter X} (hF : F.HasBasis p s) :
    ClusterPt x F â†” âˆ€ i, p i â†’ x âˆˆ closure (s i) := by
  simp only [(nhds_basis_opens _).clusterPt_iff hF, mem_closure_iff]
  tauto

theorem clusterPt_iff_forall_mem_closure {F : Filter X} :
    ClusterPt x F â†” âˆ€ s âˆˆ F, x âˆˆ closure s :=
  F.basis_sets.clusterPt_iff_forall_mem_closure

alias âŸ¨ClusterPt.mem_closure_of_mem, _âŸ© := clusterPt_iff_forall_mem_closure

/-- `x` is a cluster point of a set `s` if every neighbourhood of `x` meets `s` on a nonempty
set. See also `mem_closure_iff_clusterPt`. -/
theorem clusterPt_principal_iff :
    ClusterPt x (ğ“Ÿ s) â†” âˆ€ U âˆˆ ğ“ x, (U âˆ© s).Nonempty :=
  inf_principal_neBot_iff

theorem clusterPt_principal_iff_frequently :
    ClusterPt x (ğ“Ÿ s) â†” âˆƒá¶  y in ğ“ x, y âˆˆ s := by
  simp only [clusterPt_principal_iff, frequently_iff, Set.Nonempty, mem_inter_iff]

theorem ClusterPt.of_le_nhds {f : Filter X} (H : f â‰¤ ğ“ x) [NeBot f] : ClusterPt x f := by
  rwa [ClusterPt, inf_eq_right.mpr H]

theorem ClusterPt.of_le_nhds' {f : Filter X} (H : f â‰¤ ğ“ x) (_hf : NeBot f) :
    ClusterPt x f :=
  ClusterPt.of_le_nhds H

theorem ClusterPt.of_nhds_le {f : Filter X} (H : ğ“ x â‰¤ f) : ClusterPt x f := by
  simp only [ClusterPt, inf_eq_left.mpr H, nhds_neBot]

theorem ClusterPt.mono {f g : Filter X} (H : ClusterPt x f) (h : f â‰¤ g) : ClusterPt x g :=
  NeBot.mono H <| inf_le_inf_left _ h

theorem ClusterPt.of_inf_left {f g : Filter X} (H : ClusterPt x <| f âŠ“ g) : ClusterPt x f :=
  H.mono inf_le_left

theorem ClusterPt.of_inf_right {f g : Filter X} (H : ClusterPt x <| f âŠ“ g) :
    ClusterPt x g :=
  H.mono inf_le_right

section MapClusterPt

variable {F : Filter Î±} {u : Î± â†’ X} {x : X}

theorem mapClusterPt_def : MapClusterPt x F u â†” ClusterPt x (map u F) := Iff.rfl
alias âŸ¨MapClusterPt.clusterPt, _âŸ© := mapClusterPt_def

theorem Filter.EventuallyEq.mapClusterPt_iff {v : Î± â†’ X} (h : u =á¶ [F] v) :
    MapClusterPt x F u â†” MapClusterPt x F v := by
  simp only [mapClusterPt_def, map_congr h]

alias âŸ¨MapClusterPt.congrFun, _âŸ© := Filter.EventuallyEq.mapClusterPt_iff

theorem MapClusterPt.mono {G : Filter Î±} (h : MapClusterPt x F u) (hle : F â‰¤ G) :
    MapClusterPt x G u :=
  h.clusterPt.mono (map_mono hle)

theorem MapClusterPt.tendsto_comp' [TopologicalSpace Y] {f : X â†’ Y} {y : Y}
    (hf : Tendsto f (ğ“ x âŠ“ map u F) (ğ“ y)) (hu : MapClusterPt x F u) : MapClusterPt y F (f âˆ˜ u) :=
  (tendsto_inf.2 âŸ¨hf, tendsto_map.mono_left inf_le_rightâŸ©).neBot (hx := hu)

theorem MapClusterPt.tendsto_comp [TopologicalSpace Y] {f : X â†’ Y} {y : Y}
    (hf : Tendsto f (ğ“ x) (ğ“ y)) (hu : MapClusterPt x F u) : MapClusterPt y F (f âˆ˜ u) :=
  hu.tendsto_comp' (hf.mono_left inf_le_left)

theorem MapClusterPt.continuousAt_comp [TopologicalSpace Y] {f : X â†’ Y} (hf : ContinuousAt f x)
    (hu : MapClusterPt x F u) : MapClusterPt (f x) F (f âˆ˜ u) :=
  hu.tendsto_comp hf

theorem Filter.HasBasis.mapClusterPt_iff_frequently {Î¹ : Sort*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set X}
    (hx : (ğ“ x).HasBasis p s) : MapClusterPt x F u â†” âˆ€ i, p i â†’ âˆƒá¶  a in F, u a âˆˆ s i := by
  simp_rw [MapClusterPt, hx.clusterPt_iff_frequently, frequently_map]

theorem mapClusterPt_iff_frequently : MapClusterPt x F u â†” âˆ€ s âˆˆ ğ“ x, âˆƒá¶  a in F, u a âˆˆ s :=
  (ğ“ x).basis_sets.mapClusterPt_iff_frequently

@[deprecated (since := "2025-03-16")]
alias mapClusterPt_iff := mapClusterPt_iff_frequently

theorem MapClusterPt.frequently (h : MapClusterPt x F u) {p : X â†’ Prop} (hp : âˆ€á¶  y in ğ“ x, p y) :
    âˆƒá¶  a in F, p (u a) :=
  h.clusterPt.frequently hp

theorem mapClusterPt_comp {Ï† : Î± â†’ Î²} {u : Î² â†’ X} :
    MapClusterPt x F (u âˆ˜ Ï†) â†” MapClusterPt x (map Ï† F) u := Iff.rfl

theorem Filter.Tendsto.mapClusterPt [NeBot F] (h : Tendsto u F (ğ“ x)) : MapClusterPt x F u :=
  .of_le_nhds h

theorem MapClusterPt.of_comp {Ï† : Î² â†’ Î±} {p : Filter Î²} (h : Tendsto Ï† p F)
    (H : MapClusterPt x p (u âˆ˜ Ï†)) : MapClusterPt x F u :=
  H.clusterPt.mono <| map_mono h

end MapClusterPt

theorem accPt_sup {x : X} {F G : Filter X} :
    AccPt x (F âŠ” G) â†” AccPt x F âˆ¨ AccPt x G := by
  simp only [AccPt, inf_sup_left, sup_neBot]

theorem accPt_iff_clusterPt {x : X} {F : Filter X} : AccPt x F â†” ClusterPt x (ğ“Ÿ {x}á¶œ âŠ“ F) := by
  rw [AccPt, nhdsWithin, ClusterPt, inf_assoc]

@[deprecated (since := "2025-04-20")]
alias acc_iff_cluster := accPt_iff_clusterPt

/-- `x` is an accumulation point of a set `C` iff it is a cluster point of `C âˆ– {x}`. -/
theorem accPt_principal_iff_clusterPt {x : X} {C : Set X} :
    AccPt x (ğ“Ÿ C) â†” ClusterPt x (ğ“Ÿ (C \ { x })) := by
  rw [accPt_iff_clusterPt, inf_principal, inter_comm, diff_eq]

@[deprecated (since := "2025-04-20")]
alias acc_principal_iff_cluster := accPt_principal_iff_clusterPt

/-- `x` is an accumulation point of a set `C` iff every neighborhood
of `x` contains a point of `C` other than `x`. -/
theorem accPt_iff_nhds {x : X} {C : Set X} : AccPt x (ğ“Ÿ C) â†” âˆ€ U âˆˆ ğ“ x, âˆƒ y âˆˆ U âˆ© C, y â‰  x := by
  simp [accPt_principal_iff_clusterPt, clusterPt_principal_iff, Set.Nonempty,
    and_assoc]

/-- `x` is an accumulation point of a set `C` iff
there are points near `x` in `C` and different from `x`. -/
theorem accPt_iff_frequently {x : X} {C : Set X} : AccPt x (ğ“Ÿ C) â†” âˆƒá¶  y in ğ“ x, y â‰  x âˆ§ y âˆˆ C := by
  simp [accPt_principal_iff_clusterPt, clusterPt_principal_iff_frequently, and_comm]

/--
Variant of `accPt_iff_frequently`: A point `x` is an accumulation point of a set `C` iff points in
punctured neighborhoods are frequently contained in `C`.
-/
theorem accPt_iff_frequently_nhdsNE {X : Type*} [TopologicalSpace X] {x : X} {C : Set X} :
    AccPt x (ğ“Ÿ C) â†” âˆƒá¶  (y : X) in ğ“[â‰ ] x, y âˆˆ C := by
  have : (âˆƒá¶  z in ğ“[â‰ ] x, z âˆˆ C) â†” âˆƒá¶  z in ğ“ x, z âˆˆ C âˆ§ z âˆˆ ({x} : Set X)á¶œ :=
    frequently_inf_principal.trans <| by simp only [and_comm]
  rw [accPt_iff_frequently, this]
  congr! 2
  tauto

theorem accPt_principal_iff_nhdsWithin : AccPt x (ğ“Ÿ s) â†” (ğ“[s \ {x}] x).NeBot := by
  rw [accPt_principal_iff_clusterPt, ClusterPt, nhdsWithin]

/-- If `x` is an accumulation point of `F` and `F â‰¤ G`, then
`x` is an accumulation point of `G`. -/
theorem AccPt.mono {F G : Filter X} (h : AccPt x F) (hFG : F â‰¤ G) : AccPt x G :=
  NeBot.mono h (inf_le_inf_left _ hFG)

theorem AccPt.clusterPt {x : X} {F : Filter X} (h : AccPt x F) : ClusterPt x F :=
  (accPt_iff_clusterPt.mp h).mono inf_le_right

theorem clusterPt_principal {x : X} {C : Set X} :
    ClusterPt x (ğ“Ÿ C) â†” x âˆˆ C âˆ¨ AccPt x (ğ“Ÿ C) := by
  constructor
  Â· intro h
    by_contra! hc
    rw [accPt_principal_iff_clusterPt] at hc
    simp_all only [not_false_eq_true, diff_singleton_eq_self, not_true_eq_false, hc.1]
  Â· rintro (h | h)
    Â· exact clusterPt_principal_iff.mpr fun _ mem â†¦ âŸ¨x, âŸ¨mem_of_mem_nhds mem, hâŸ©âŸ©
    Â· exact h.clusterPt

/-- The set of cluster points of a filter is closed. In particular, the set of limit points
of a sequence is closed. -/
theorem isClosed_setOf_clusterPt {f : Filter X} : IsClosed { x | ClusterPt x f } := by
  simp only [clusterPt_iff_forall_mem_closure, setOf_forall]
  exact isClosed_biInter fun _ _ â†¦ isClosed_closure

theorem mem_closure_iff_clusterPt : x âˆˆ closure s â†” ClusterPt x (ğ“Ÿ s) :=
  mem_closure_iff_frequently.trans clusterPt_principal_iff_frequently.symm

alias âŸ¨_, ClusterPt.mem_closureâŸ© := mem_closure_iff_clusterPt

theorem mem_closure_iff_nhds_ne_bot : x âˆˆ closure s â†” ğ“ x âŠ“ ğ“Ÿ s â‰  âŠ¥ :=
  mem_closure_iff_clusterPt.trans neBot_iff

theorem mem_closure_iff_nhdsWithin_neBot : x âˆˆ closure s â†” NeBot (ğ“[s] x) :=
  mem_closure_iff_clusterPt

lemma notMem_closure_iff_nhdsWithin_eq_bot : x âˆ‰ closure s â†” ğ“[s] x = âŠ¥ := by
  rw [mem_closure_iff_nhdsWithin_neBot, not_neBot]

@[deprecated (since := "2025-05-23")]
alias not_mem_closure_iff_nhdsWithin_eq_bot := notMem_closure_iff_nhdsWithin_eq_bot

theorem mem_interior_iff_not_clusterPt_compl : x âˆˆ interior s â†” Â¬ClusterPt x (ğ“Ÿ sá¶œ) := by
  rw [â† mem_closure_iff_clusterPt, closure_compl, mem_compl_iff, not_not]

/-- If `x` is not an isolated point of a topological space, then `{x}á¶œ` is dense in the whole
space. -/
theorem dense_compl_singleton (x : X) [NeBot (ğ“[â‰ ] x)] : Dense ({x}á¶œ : Set X) := by
  intro y
  rcases eq_or_ne y x with (rfl | hne)
  Â· rwa [mem_closure_iff_nhdsWithin_neBot]
  Â· exact subset_closure hne

/-- If `x` is not an isolated point of a topological space, then the closure of `{x}á¶œ` is the whole
space. -/
theorem closure_compl_singleton (x : X) [NeBot (ğ“[â‰ ] x)] : closure {x}á¶œ = (univ : Set X) :=
  (dense_compl_singleton x).closure_eq

/-- If `x` is not an isolated point of a topological space, then the interior of `{x}` is empty. -/
@[simp]
theorem interior_singleton (x : X) [NeBot (ğ“[â‰ ] x)] : interior {x} = (âˆ… : Set X) :=
  interior_eq_empty_iff_dense_compl.2 (dense_compl_singleton x)

theorem not_isOpen_singleton (x : X) [NeBot (ğ“[â‰ ] x)] : Â¬IsOpen ({x} : Set X) :=
  dense_compl_singleton_iff_not_open.1 (dense_compl_singleton x)

theorem closure_eq_cluster_pts : closure s = { a | ClusterPt a (ğ“Ÿ s) } :=
  Set.ext fun _ => mem_closure_iff_clusterPt

theorem mem_closure_iff_nhds : x âˆˆ closure s â†” âˆ€ t âˆˆ ğ“ x, (t âˆ© s).Nonempty :=
  mem_closure_iff_clusterPt.trans clusterPt_principal_iff

theorem mem_closure_iff_nhds' : x âˆˆ closure s â†” âˆ€ t âˆˆ ğ“ x, âˆƒ y : s, â†‘y âˆˆ t := by
  simp only [mem_closure_iff_nhds, Set.inter_nonempty_iff_exists_right, SetCoe.exists, exists_prop]

theorem mem_closure_iff_comap_neBot :
    x âˆˆ closure s â†” NeBot (comap ((â†‘) : s â†’ X) (ğ“ x)) := by
  simp_rw [mem_closure_iff_nhds, comap_neBot_iff, Set.inter_nonempty_iff_exists_right,
    SetCoe.exists, exists_prop]

theorem mem_closure_iff_nhds_basis' {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set X} (h : (ğ“ x).HasBasis p s) :
    x âˆˆ closure t â†” âˆ€ i, p i â†’ (s i âˆ© t).Nonempty :=
  mem_closure_iff_clusterPt.trans <|
    (h.clusterPt_iff (hasBasis_principal _)).trans <| by simp only [forall_const]

theorem mem_closure_iff_nhds_basis {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set X} (h : (ğ“ x).HasBasis p s) :
    x âˆˆ closure t â†” âˆ€ i, p i â†’ âˆƒ y âˆˆ t, y âˆˆ s i :=
  (mem_closure_iff_nhds_basis' h).trans <| by
    simp only [Set.Nonempty, mem_inter_iff, and_comm]

theorem clusterPt_iff_lift'_closure {F : Filter X} :
    ClusterPt x F â†” pure x â‰¤ (F.lift' closure) := by
  simp_rw [clusterPt_iff_forall_mem_closure,
    (hasBasis_pure _).le_basis_iff F.basis_sets.lift'_closure, id, singleton_subset_iff, true_and,
    exists_const]

theorem clusterPt_iff_lift'_closure' {F : Filter X} :
    ClusterPt x F â†” (F.lift' closure âŠ“ pure x).NeBot := by
  rw [clusterPt_iff_lift'_closure, inf_comm]
  constructor
  Â· intro h
    simp [h, pure_neBot]
  Â· intro h U hU
    simp_rw [â† forall_mem_nonempty_iff_neBot, mem_inf_iff] at h
    simpa using h ({x} âˆ© U) âŸ¨{x}, by simp, U, hU, rflâŸ©

@[simp]
theorem clusterPt_lift'_closure_iff {F : Filter X} :
    ClusterPt x (F.lift' closure) â†” ClusterPt x F := by
  simp [clusterPt_iff_lift'_closure, lift'_lift'_assoc (monotone_closure X) (monotone_closure X)]

theorem isClosed_iff_clusterPt : IsClosed s â†” âˆ€ a, ClusterPt a (ğ“Ÿ s) â†’ a âˆˆ s :=
  calc
    IsClosed s â†” closure s âŠ† s := closure_subset_iff_isClosed.symm
    _ â†” âˆ€ a, ClusterPt a (ğ“Ÿ s) â†’ a âˆˆ s := by simp only [subset_def, mem_closure_iff_clusterPt]

theorem isClosed_iff_nhds :
    IsClosed s â†” âˆ€ x, (âˆ€ U âˆˆ ğ“ x, (U âˆ© s).Nonempty) â†’ x âˆˆ s := by
  simp_rw [isClosed_iff_clusterPt, ClusterPt, inf_principal_neBot_iff]

lemma isClosed_iff_forall_filter :
    IsClosed s â†” âˆ€ x, âˆ€ F : Filter X, F.NeBot â†’ F â‰¤ ğ“Ÿ s â†’ F â‰¤ ğ“ x â†’ x âˆˆ s := by
  simp_rw [isClosed_iff_clusterPt]
  exact âŸ¨fun hs x F F_ne FS Fx â†¦ hs _ <| NeBot.mono F_ne (le_inf Fx FS),
         fun hs x hx â†¦ hs x (ğ“ x âŠ“ ğ“Ÿ s) hx inf_le_right inf_le_leftâŸ©

theorem mem_closure_of_mem_closure_union (h : x âˆˆ closure (sâ‚ âˆª sâ‚‚))
    (hâ‚ : sâ‚á¶œ âˆˆ ğ“ x) : x âˆˆ closure sâ‚‚ := by
  rw [mem_closure_iff_nhds_ne_bot] at *
  rwa [â† sup_principal, inf_sup_left, inf_principal_eq_bot.mpr hâ‚, bot_sup_eq] at h
