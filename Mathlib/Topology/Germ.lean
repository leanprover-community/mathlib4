/-
Copyright (c) 2023 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
import Mathlib.Order.Filter.Germ
import Mathlib.Topology.Algebra.Ring.Basic
import Mathlib.Analysis.Calculus.FDeriv.Basic
import Mathlib.Algebra.Order.Hom.Ring
import Mathlib.Topology.NhdsSet
import Mathlib.Topology.LocallyConstant.Basic

/-! # Germs of functions between topological spaces

In this file, we prove basic properties of germs of functions between topological spaces,
with respect to the neighbourhood filter `ğ“ x`.

## Main definitions and results
* `Filter.Germ.value Ï† f`: value associated to the germ `Ï†` at a point `x`, w.r.t. the neighbourhood
filter at `x`. This is the common value of all representatives of `Ï†` at `x`.
* `Filter.Germ.valueOrderRingHom` and friends: the map `Germ (ğ“ x) E â†’ E` is a
monoid homomorphism, ğ•œ-module homomorphism, ring homomorphism, monotone ring homeomorphism

* `RestrictGermPredicate`: given a predicate on germs `P : Î  x : X, germ (ğ“ x) Y â†’ Prop` and
`A : set X`, build a new predicate on germs `restrict_germ_predicate P A` such that
`(âˆ€ x, restrict_germ_predicate P A x f) â†” âˆ€á¶  x near A, P x f`;
`forall_restrict_germ_predicate_iff` is this equivalence.

* `Filter.Germ.sliceLeft,sliceRight`: map the germ of functions `X Ã— Y â†’ Z` at `p=(x,y) âˆˆ X Ã— Y` to
the corresponding germ of functions `X â†’ Z` at `x âˆˆ X` resp. `Y â†’ Z` at `y âˆˆ Y`..
* `eq_of_germ_isConstant`: if each germ of `f : X â†’ Y` is constant and `X` is pre-connected,
`f` is constant.
-/

variable {F G : Type*} [NormedAddCommGroup F] [NormedSpace â„ F]
  [NormedAddCommGroup G] [NormedSpace â„ G]

open scoped Topology

open Filter Set

variable {X Y Z : Type*} [TopologicalSpace X] {f g : X â†’ Y} {A : Set X} {x : X}

namespace Filter.Germ

/-- The value associated to a germ at a point. This is the common value
shared by all representatives at the given point. -/
def value {X Î± : Type*} [TopologicalSpace X] {x : X} (Ï† : Germ (ğ“ x) Î±) : Î± :=
  Quotient.liftOn' Ï† (fun f â†¦ f x) fun f g h â†¦ by dsimp only; rw [Eventually.self_of_nhds h]

theorem value_smul {Î± Î² : Type*} [SMul Î± Î²] (Ï† : Germ (ğ“ x) Î±)
    (Ïˆ : Germ (ğ“ x) Î²) : (Ï† â€¢ Ïˆ).value = Ï†.value â€¢ Ïˆ.value :=
  Germ.inductionOn Ï† fun _ â†¦ Germ.inductionOn Ïˆ fun _ â†¦ rfl

/-- The map `Germ (ğ“ x) E â†’ E` as a monoid homeomorphism -/
@[to_additive "The map `Germ (ğ“ x) E â†’ E` as an additive monoid homeomorphism"]
def valueMulHom {X E : Type*} [Monoid E] [TopologicalSpace X] {x : X} : Germ (ğ“ x) E â†’* E where
  toFun := Filter.Germ.value
  map_one' := rfl
  map_mul' Ï† Ïˆ := Germ.inductionOn Ï† fun _ â†¦ Germ.inductionOn Ïˆ fun _ â†¦ rfl

/-- The map `Germ (ğ“ x) E â†’ E` into a `ğ•œ`-module `E` as a `ğ•œ`-linear map -/
def valueâ‚— {X ğ•œ E : Type*} [Semiring ğ•œ] [AddCommMonoid E] [Module ğ•œ E] [TopologicalSpace X]
    {x : X} : Germ (ğ“ x) E â†’â‚—[ğ•œ] E :=
  { Filter.Germ.valueAddHom with map_smul' := fun _ Ï† â†¦ Germ.inductionOn Ï† fun _ â†¦ rfl }

/-- The map `Germ (ğ“ x) E â†’ E` as a ring homeomorphism -/
def valueRingHom {X E : Type*} [Semiring E] [TopologicalSpace X] {x : X} : Germ (ğ“ x) E â†’+* E :=
  { Filter.Germ.valueMulHom, Filter.Germ.valueAddHom with }

/-- The map `Germ (ğ“ x) E â†’ E` as a monotone ring homeomorphism -/
def valueOrderRingHom {X E : Type*} [OrderedSemiring E] [TopologicalSpace X] {x : X} :
    Germ (ğ“ x) E â†’+*o E where
  __ := Filter.Germ.valueRingHom
  monotone' := fun Ï† Ïˆ â†¦
  Germ.inductionOn Ï† fun _ â†¦ Germ.inductionOn Ïˆ fun _ h â†¦ h.self_of_nhds

end Filter.Germ

/-- The inclusion `S â†’ R` of a subring, as an ordered ring homomorphism. -/
-- xxx: OrderedRing has no morphisms, OrderedRingHom no subtypes -> which file is a good place?
def _root_.Subring.orderedSubtype {R} [OrderedRing R] (s : Subring R) : s â†’+*o R :=
  { s.subtype with monotone' := fun _ _ h â†¦ h }

section RestrictGermPredicate
/-- Given a predicate on germs `P : Î  x : X, germ (ğ“ x) Y â†’ Prop` and `A : set X`,
build a new predicate on germs `restrict_germ_predicate P A` such that
`(âˆ€ x, restrict_germ_predicate P A x f) â†” âˆ€á¶  x near A, P x f`, see
`forall_restrict_germ_predicate_iff` for this equivalence. -/
def RestrictGermPredicate (P : âˆ€ x : X, Germ (ğ“ x) Y â†’ Prop)
    (A : Set X) : âˆ€ x : X, Germ (ğ“ x) Y â†’ Prop := fun x Ï† â†¦
  Germ.liftOn Ï† (fun f â†¦ x âˆˆ A â†’ âˆ€á¶  y in ğ“ x, P y f)
    haveI : âˆ€ f f' : X â†’ Y, f =á¶ [ğ“ x] f' â†’ (âˆ€á¶  y in ğ“ x, P y f) â†’ âˆ€á¶  y in ğ“ x, P y f' := by
      intro f f' hff' hf
      apply (hf.and <| Eventually.eventually_nhds hff').mono
      rintro y âŸ¨hy, hy'âŸ©
      rwa [Germ.coe_eq.mpr (EventuallyEq.symm hy')]
    fun f f' hff' â†¦ propext <| forall_congr' fun _ â†¦ âŸ¨this f f' hff', this f' f hff'.symmâŸ©

theorem Filter.Eventually.germ_congr_set
    {P : âˆ€ x : X, Germ (ğ“ x) Y â†’ Prop} (hf : âˆ€á¶  x in ğ“Ë¢ A, P x f)
    (h : âˆ€á¶  z in ğ“Ë¢ A, g z = f z) : âˆ€á¶  x in ğ“Ë¢ A, P x g := by
  rw [eventually_nhdsSet_iff_forall] at *
  intro x hx
  apply ((hf x hx).and (h x hx).eventually_nhds).mono
  intro y hy
  convert hy.1 using 1
  exact Germ.coe_eq.mpr hy.2

theorem restrictGermPredicate_congr {P : âˆ€ x : X, Germ (ğ“ x) Y â†’ Prop}
    (hf : RestrictGermPredicate P A x f) (h : âˆ€á¶  z in ğ“Ë¢ A, g z = f z) :
    RestrictGermPredicate P A x g := by
  intro hx
  apply ((hf hx).and <| (eventually_nhdsSet_iff_forall.mp h x hx).eventually_nhds).mono
  rintro y âŸ¨hy, h'yâŸ©
  rwa [Germ.coe_eq.mpr h'y]

theorem forall_restrictGermPredicate_iff {P : âˆ€ x : X, Germ (ğ“ x) Y â†’ Prop} :
    (âˆ€ x, RestrictGermPredicate P A x f) â†” âˆ€á¶  x in ğ“Ë¢ A, P x f := by
  rw [eventually_nhdsSet_iff_forall]
  rfl

theorem forall_restrictGermPredicate_of_forall
    {P : âˆ€ x : X, Germ (ğ“ x) Y â†’ Prop} (h : âˆ€ x, P x f) :
    âˆ€ x, RestrictGermPredicate P A x f :=
  forall_restrictGermPredicate_iff.mpr (eventually_of_forall h)
end RestrictGermPredicate

namespace Filter.Germ
/-- Map the germ at of functions `X Ã— Y â†’ Z` at `p=(x,y) âˆˆ X Ã— Y` to the corresponding germ
  of functions `X â†’ Z` at `x âˆˆ X` -/
def sliceLeft [TopologicalSpace Y] {p : X Ã— Y} (P : Germ (ğ“ p) Z) : Germ (ğ“ p.1) Z :=
  P.compTendsto (Prod.mk Â· p.2) (Continuous.Prod.mk_left p.2).continuousAt

@[simp]
theorem sliceLeft_coe [TopologicalSpace Y] {y : Y} (f : X Ã— Y â†’ Z) :
    (â†‘f : Germ (ğ“ (x, y)) Z).sliceLeft = fun x' â†¦ f (x', y) :=
  rfl

/-- Map the germ at of functions `X Ã— Y â†’ Z` at `p=(x,y) âˆˆ X Ã— Y` to the corresponding germ
  of functions `Y â†’ Z` at `y âˆˆ Y` -/
def sliceRight [TopologicalSpace Y] {p : X Ã— Y} (P : Germ (ğ“ p) Z) : Germ (ğ“ p.2) Z :=
  P.compTendsto (Prod.mk p.1) (Continuous.Prod.mk p.1).continuousAt

@[simp]
theorem sliceRight_coe [TopologicalSpace Y] {y : Y} (f : X Ã— Y â†’ Z) :
    (â†‘f : Germ (ğ“ (x, y)) Z).sliceRight = fun y' â†¦ f (x, y') :=
  rfl

end Filter.Germ

/-- If the germ of `f` w.r.t. each `ğ“ x` is constant, `f` is locally constant. -/
private lemma IsLocallyConstant.of_germ_isConstant (h : âˆ€ x : X, (f : Germ (ğ“ x) Y).IsConstant) :
    IsLocallyConstant f := by
  intro s
  rw [isOpen_iff_mem_nhds]
  intro a ha
  obtain âŸ¨b, hbâŸ© := h a
  apply mem_of_superset hb
  intro x hx
  have : f x = f a := (mem_of_mem_nhds hb) â–¸ hx
  rw [mem_preimage, this]
  exact ha

-- should follow from `isConstant_compTendsto` specialised to the neigbourhood filter
proof_wanted isConstant_comp_subtype {s : Set X} {f : X â†’ Y} {x : s}
  (_hf : (f : Germ (ğ“ (x : X)) Y).IsConstant) :
    ((f âˆ˜ Subtype.val : s â†’ Y) : Germ (ğ“ x) Y).IsConstant

-- move to `LocallyConstant/Basic.lean` once proven
proof_wanted IsLocallyConstant.of_comp_of_inducing
  {f : X â†’ Y} [TopologicalSpace X] [TopologicalSpace Y] [TopologicalSpace Z]
  {i : Z â†’ X} (_hi : Inducing i) (_hF : IsLocallyConstant (f âˆ˜ i)) : IsLocallyConstant f

-- use previous lemma: `Subtype.val` is inducing
proof_wanted IsLocallyConstant.of_germ_isConstantOn_of_preconnected {s : Set X} [TopologicalSpace Y]
    (_hs : IsPreconnected s) (_h : âˆ€ x âˆˆ s, (f : Germ (ğ“ x) Y).IsConstant) : IsLocallyConstant f

theorem eq_of_germ_isConstant [i: PreconnectedSpace X]
    (h : âˆ€ x : X, (f : Germ (ğ“ x) Y).IsConstant) (x x' : X) : f x = f x' :=
  (IsLocallyConstant.of_germ_isConstant h).apply_eq_of_isPreconnected
    (preconnectedSpace_iff_univ.mp i) (by trivial) (by trivial)

-- use `IsLocallyConstant.of_germ_isConstantOn_of_preconnected`
proof_wanted eq_of_germ_isConstant_on {s : Set X} (_h : âˆ€ x âˆˆ s, (f : Germ (ğ“ x) Y).IsConstant)
    (_hs : IsPreconnected s) {x' : X} (_x_in : x âˆˆ s) (_x'_in : x' âˆˆ s) : f x = f x'

open scoped BigOperators in
@[to_additive (attr := simp)]
theorem Germ.coe_prod {Î± : Type*} (l : Filter Î±) (R : Type*) [CommMonoid R] {Î¹} (f : Î¹ â†’ Î± â†’ R)
    (s : Finset Î¹) : ((âˆ i in s, f i : Î± â†’ R) : Germ l R) = âˆ i in s, (f i : Germ l R) :=
  map_prod (Germ.coeMulHom l : (Î± â†’ R) â†’* Germ l R) f s
