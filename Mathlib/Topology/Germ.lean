/-
Copyright (c) 2023 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
import Mathlib.Order.Filter.Germ
import Mathlib.Topology.Algebra.Ring.Basic
import Mathlib.Analysis.Calculus.FDeriv.Basic
import Mathlib.Algebra.Order.Hom.Ring
import Mathlib.Topology.NhdsSet

/-! ## Additional lemmas about germs of functions

TODO: add a module docstring, eventually
-/

variable {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F]

variable {G : Type*} [NormedAddCommGroup G] [NormedSpace â„ G]

open scoped Topology

open Filter Set

namespace Filter.Germ

/-- The value associated to a germ at a point. This is the common value
shared by all representatives at the given point. -/
def value {X Î± : Type*} [TopologicalSpace X] {x : X} (Ï† : Germ (ğ“ x) Î±) : Î± :=
  Quotient.liftOn' Ï† (fun f => f x) fun f g h => by dsimp only; rw [Eventually.self_of_nhds h]

theorem value_smul {X Î± Î² : Type*} [TopologicalSpace X] {x : X} [SMul Î± Î²] (Ï† : Germ (ğ“ x) Î±)
    (Ïˆ : Germ (ğ“ x) Î²) : (Ï† â€¢ Ïˆ).value = Ï†.value â€¢ Ïˆ.value :=
  Germ.inductionOn Ï† fun _ => Germ.inductionOn Ïˆ fun _ => rfl

@[to_additive]
def valueMulHom {X E : Type*} [Monoid E] [TopologicalSpace X] {x : X} : Germ (ğ“ x) E â†’* E
    where
  toFun := Filter.Germ.value
  map_one' := rfl
  map_mul' Ï† Ïˆ := Germ.inductionOn Ï† fun _ => Germ.inductionOn Ïˆ fun _ => rfl

def valueâ‚— {X ğ•œ E : Type*} [Semiring ğ•œ] [AddCommMonoid E] [Module ğ•œ E] [TopologicalSpace X]
    {x : X} : Germ (ğ“ x) E â†’â‚—[ğ•œ] E :=
  { Filter.Germ.valueAddHom with map_smul' := fun _ Ï† => Germ.inductionOn Ï† fun _ => rfl }

def valueRingHom {X E : Type*} [Semiring E] [TopologicalSpace X] {x : X} : Germ (ğ“ x) E â†’+* E :=
  { Filter.Germ.valueMulHom, Filter.Germ.valueAddHom with }

def valueOrderRingHom {X E : Type*} [OrderedSemiring E] [TopologicalSpace X] {x : X} :
    Germ (ğ“ x) E â†’+*o E :=
  { Filter.Germ.valueRingHom with
    monotone' := fun Ï† Ïˆ =>
      Germ.inductionOn Ï† fun _ => Germ.inductionOn Ïˆ fun _ h => h.self_of_nhds }

def _root_.Subring.orderedSubtype {R} [OrderedRing R] (s : Subring R) : s â†’+*o R :=
  { s.subtype with monotone' := fun _ _ h => h }

end Filter.Germ

/-- Given a predicate on germs `P : Î  x : X, germ (ğ“ x) Y â†’ Prop` and `A : set X`,
build a new predicate on germs `restrict_germ_predicate P A` such that
`(âˆ€ x, restrict_germ_predicate P A x f) â†” âˆ€á¶  x near A, P x f`, see
`forall_restrict_germ_predicate_iff` for this equivalence. -/
def RestrictGermPredicate {X Y : Type*} [TopologicalSpace X] (P : âˆ€ x : X, Germ (ğ“ x) Y â†’ Prop)
    (A : Set X) : âˆ€ x : X, Germ (ğ“ x) Y â†’ Prop := fun x Ï† =>
  Quotient.liftOn' Ï† (fun f => x âˆˆ A â†’ âˆ€á¶  y in ğ“ x, P y f)
    haveI : âˆ€ f f' : X â†’ Y, f =á¶ [ğ“ x] f' â†’ (âˆ€á¶  y in ğ“ x, P y f) â†’ âˆ€á¶  y in ğ“ x, P y f' := by
      intro f f' hff' hf
      apply (hf.and <| Eventually.eventually_nhds hff').mono
      rintro y âŸ¨hy, hy'âŸ©
      rwa [Germ.coe_eq.mpr (EventuallyEq.symm hy')]
    fun f f' hff' => propext <| forall_congr' fun _ => âŸ¨this f f' hff', this f' f hff'.symmâŸ©

theorem Filter.Eventually.germ_congr {X Y : Type*} [TopologicalSpace X] {x : X}
    {P : Germ (ğ“ x) Y â†’ Prop} {f g : X â†’ Y} (hf : P f) (h : âˆ€á¶  z in ğ“ x, g z = f z) : P g := by
  convert hf using 1
  apply Quotient.sound
  exact h

theorem Filter.Eventually.germ_congr_set {X Y : Type*} [TopologicalSpace X]
    {P : âˆ€ x : X, Germ (ğ“ x) Y â†’ Prop} {A : Set X} {f g : X â†’ Y} (hf : âˆ€á¶  x in ğ“Ë¢ A, P x f)
    (h : âˆ€á¶  z in ğ“Ë¢ A, g z = f z) : âˆ€á¶  x in ğ“Ë¢ A, P x g := by
  rw [eventually_nhdsSet_iff] at *
  intro x hx
  apply ((hf x hx).and (h x hx).eventually_nhds).mono
  exact fun y hy => hy.2.germ_congr hy.1

theorem restrictGermPredicate_congr {X Y : Type*} [TopologicalSpace X]
    {P : âˆ€ x : X, Germ (ğ“ x) Y â†’ Prop} {A : Set X} {f g : X â†’ Y} {x : X}
    (hf : RestrictGermPredicate P A x f) (h : âˆ€á¶  z in ğ“Ë¢ A, g z = f z) :
    RestrictGermPredicate P A x g := by
  intro hx
  apply ((hf hx).and <| (eventually_nhdsSet_iff.mp h x hx).eventually_nhds).mono
  rintro y âŸ¨hy, h'yâŸ©
  rwa [Germ.coe_eq.mpr h'y]

theorem forall_restrictGermPredicate_iff {X Y : Type*} [TopologicalSpace X]
    {P : âˆ€ x : X, Germ (ğ“ x) Y â†’ Prop} {A : Set X} {f : X â†’ Y} :
    (âˆ€ x, RestrictGermPredicate P A x f) â†” âˆ€á¶  x in ğ“Ë¢ A, P x f := by
  rw [eventually_nhdsSet_iff]
  rfl

theorem forall_restrictGermPredicate_of_forall {X Y : Type*} [TopologicalSpace X]
    {P : âˆ€ x : X, Germ (ğ“ x) Y â†’ Prop} {A : Set X} {f : X â†’ Y} (h : âˆ€ x, P x f) :
    âˆ€ x, RestrictGermPredicate P A x f :=
  forall_restrictGermPredicate_iff.mpr (eventually_of_forall h)

theorem Filter.EventuallyEq.comp_fun {Î± Î² Î³ : Type*} {f g : Î² â†’ Î³} {l : Filter Î±} {l' : Filter Î²}
    (h : f =á¶ [l'] g) {Ï† : Î± â†’ Î²} (hÏ† : Tendsto Ï† l l') : f âˆ˜ Ï† =á¶ [l] g âˆ˜ Ï† :=
  hÏ† h

theorem Filter.Tendsto.congr_germ {Î± Î² Î³ : Type*} {f g : Î² â†’ Î³} {l : Filter Î±} {l' : Filter Î²}
    (h : f =á¶ [l'] g) {Ï† : Î± â†’ Î²} (hÏ† : Tendsto Ï† l l') : (f âˆ˜ Ï† : Germ l Î³) = g âˆ˜ Ï† :=
  @Quotient.sound _ (l.germSetoid Î³) _ _ (hÏ† h)

def Filter.Germ.sliceLeft {X Y Z : Type*} [TopologicalSpace X] [TopologicalSpace Y] {p : X Ã— Y}
    (P : Germ (ğ“ p) Z) : Germ (ğ“ p.1) Z :=
  P.compTendsto (Prod.mk Â· p.2) (Continuous.Prod.mk_left p.2).continuousAt

@[simp]
theorem Filter.Germ.sliceLeft_coe {X Y Z : Type*} [TopologicalSpace X] [TopologicalSpace Y] {x : X}
    {y : Y} (f : X Ã— Y â†’ Z) : (â†‘f : Germ (ğ“ (x, y)) Z).sliceLeft = fun x' => f (x', y) :=
  rfl

def Filter.Germ.sliceRight {X Y Z : Type*} [TopologicalSpace X] [TopologicalSpace Y] {p : X Ã— Y}
    (P : Germ (ğ“ p) Z) : Germ (ğ“ p.2) Z :=
  P.compTendsto (Prod.mk p.1) (Continuous.Prod.mk p.1).continuousAt

@[simp]
theorem Filter.Germ.sliceRight_coe {X Y Z : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    {x : X} {y : Y} (f : X Ã— Y â†’ Z) : (â†‘f : Germ (ğ“ (x, y)) Z).sliceRight = fun y' => f (x, y') :=
  rfl

def Filter.Germ.IsConstant {X Y : Type*} [TopologicalSpace X] {x : X} (P : Germ (ğ“ x) Y) : Prop :=
  P.liftOn (fun f => âˆ€á¶  x' in ğ“ x, f x' = f x) <| by
    suffices : âˆ€ f g : X â†’ Y, f =á¶ [ğ“ x] g â†’ (âˆ€á¶  x' in ğ“ x, f x' = f x) â†’ âˆ€á¶  x' in ğ“ x, g x' = g x
    exact fun f g hfg => propext âŸ¨fun h => this f g hfg h, fun h => this g f hfg.symm hâŸ©
    rintro f g hfg hf
    refine (hf.and hfg).mono fun x' hx' => ?_
    rw [â† hx'.2, hx'.1, hfg.eq_of_nhds]

theorem Filter.Germ.isConstant_coe {X Y : Type*} [TopologicalSpace X] {x : X} {y} {f : X â†’ Y}
    (h : âˆ€ x', f x' = y) : (â†‘f : Germ (ğ“ x) Y).IsConstant :=
  eventually_of_forall fun x' => by rw [h, h]

@[simp]
theorem Filter.Germ.isConstant_coe_const {X Y : Type*} [TopologicalSpace X] {x : X} {y : Y} :
    (fun _ : X => y : Germ (ğ“ x) Y).IsConstant :=
  eventually_of_forall fun _ => rfl

theorem eq_of_germ_isConstant {X Y : Type*} [TopologicalSpace X] [PreconnectedSpace X] {f : X â†’ Y}
    (h : âˆ€ x : X, (f : Germ (ğ“ x) Y).IsConstant) (x x' : X) : f x = f x' := by
  revert x
  erw [â† eq_univ_iff_forall]
  apply IsClopen.eq_univ _ (âŸ¨x', rflâŸ© : {x | f x = f x'}.Nonempty)
  refine' âŸ¨isOpen_iff_eventually.mpr fun x hx => hx â–¸ h x, _âŸ©
  rw [isClosed_iff_frequently]
  rintro x hx
  rcases ((h x).and_frequently hx).exists with âŸ¨x'', HâŸ©
  exact H.1.symm.trans H.2

theorem eq_of_germ_isConstant_on {X Y : Type*} [TopologicalSpace X] {f : X â†’ Y} {s : Set X}
    (h : âˆ€ x âˆˆ s, (f : Germ (ğ“ x) Y).IsConstant) (hs : IsPreconnected s) {x x' : X} (x_in : x âˆˆ s)
    (x'_in : x' âˆˆ s) : f x = f x' := by
  haveI := isPreconnected_iff_preconnectedSpace.mp hs
  let F : s â†’ Y := f âˆ˜ (â†‘)
  change F âŸ¨x, x_inâŸ© = F âŸ¨x', x'_inâŸ©
  apply eq_of_germ_isConstant
  rintro âŸ¨x, hxâŸ©
  have : ContinuousAt ((â†‘) : s â†’ X) âŸ¨x, hxâŸ© := continuousAt_subtype_val
  exact this (h x hx)
