/-
Copyright (c) 2024 Elliot Dean Young and Jiazhen Xia. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jiazhen Xia, Elliot Dean Young
-/

import Mathlib.Topology.Category.TopCat.Limits.Basic
import Mathlib.CategoryTheory.Functor.OfSequence

/-!
# CW-complexes

This file defines (relative) CW-complexes.

## Main definitions

* `RelativeCWComplex`: A relative CW-complex is the colimit of an expanding sequence of subspaces
`sk i` (called the `(i-1)`-skeleton) for `i ‚â• 0`, where `sk 0` (i.e., the `(-1)`-skeleton) is an
arbitrary topological space, and each `sk (n+1)` (i.e., the `n`-skeleton) is obtained from `sk n`
(i.e., the `(n-1)`-skeleton) by attaching `n`-disks.

* `CWComplex`: A CW-complex is a relative CW-complex whose `sk 0` (i.e., `(-1)`-skeleton) is empty.

## References

The definition of CW-complexes follows David W√§rn's suggestion at
https://leanprover.zulipchat.com/#narrow/stream/217875-Is-there-code-for-X.3F/topic/Do.20we.20have.20CW.20complexes.3F/near/231769080
-/

open CategoryTheory TopCat

universe u

namespace RelativeCWComplex

/-- The inclusion map from the `n`-sphere to the `(n+1)`-disk -/
def sphereInclusion (n : ‚Ñ§) : ùïä n ‚ü∂ ùîª (n + 1) where
  toFun := fun ‚ü®p, hp‚ü© ‚Ü¶ ‚ü®p, le_of_eq hp‚ü©
  continuous_toFun := ‚ü®fun t ‚ü®s, ‚ü®r, hro, hrs‚ü©, hst‚ü© ‚Ü¶ by
    rw [isOpen_induced_iff, ‚Üê hst, ‚Üê hrs]
    tauto‚ü©

variable {S D : TopCat.{u}} (f : S ‚ü∂ D)

/-- Given the inclusion map `f : S ‚ü∂ D` for one generalized cell, we construct the inclusion map
from the disjoint union of `S` (boundary of generalized cells) to the disjoint union of `D`
(generalized cells), where both of the disjoint unions are indexed by `cells`. -/
def generalizedSigmaSphereInclusion (cells : Type) :
    TopCat.of (Œ£ (_ : cells), S) ‚ü∂ TopCat.of (Œ£ (_ : cells), D) where
  toFun := Sigma.map id fun _ x ‚Ü¶ f.toFun x
  continuous_toFun := Continuous.sigma_map fun _ ‚Ü¶ f.continuous_toFun

/-- Given an attaching map for each `S` (boundary of a generalized cell), we construct
the attaching map for the disjoint union of all the `S`. -/
def generalizedSigmaAttachMap (S X : TopCat.{u}) (cells : Type) (attach_maps : cells ‚Üí C(S, X)) :
    TopCat.of (Œ£ (_ : cells), S) ‚ü∂ X where
  toFun := fun ‚ü®i, x‚ü© ‚Ü¶ attach_maps i x
  continuous_toFun := continuous_sigma fun i ‚Ü¶ (attach_maps i).continuous_toFun

/-- A type witnessing that `X'` is obtained from `X` by attaching generalized cells `f : S ‚ü∂ D`. -/
structure AttachGeneralizedCells (X X' : TopCat.{u}) where
  /-- The index type over the generalized `(n+1)`-cells -/
  cells : Type
  /-- For each generalized `(n+1)`-cell, we have an attaching map from its boundary to `X`. -/
  attach_maps : cells ‚Üí C(S, X)
  /-- `X'` is the pushout obtained from `X` along `sigmaAttachMap`. -/
  iso_pushout : X' ‚âÖ Limits.pushout (generalizedSigmaSphereInclusion f cells)
    (generalizedSigmaAttachMap S X cells attach_maps)

/-- The inclusion map from the disjoint union of `n`-spheres to the disjoint union of `(n+1)`-disks,
where both of the disjoint unions are indexed by `cells` -/
noncomputable def sigmaSphereInclusion (n : ‚Ñ§) :=
  generalizedSigmaSphereInclusion (sphereInclusion n)

/-- Given an attaching map for each `n`-sphere, we construct the attaching map for the disjoint
union of the `n`-spheres. -/
def sigmaAttachMap (n : ‚Ñ§) := generalizedSigmaAttachMap (sphere n)

/-- A type witnessing that `X'` is obtained from `X` by attaching `(n+1)`-disks -/
def AttachCells (n : ‚Ñ§) := AttachGeneralizedCells (sphereInclusion n)

end RelativeCWComplex

/-- A relative CW-complex contains an expanding sequence of subspaces `sk i` (called the
`(i-1)`-skeleton) for `i ‚â• 0`, where `sk 0` (i.e., the `(-1)`-skeleton) is an arbitrary topological
space, and each `sk (n+1)` (i.e., the `n`-skeleton) is obtained from `sk n` (i.e., the
`(n-1)`-skeleton) by attaching `n`-disks. -/
structure RelativeCWComplex where
  /-- The skeletons. Note: `sk i` is usually called the `(i-1)`-skeleton in the math literature. -/
  sk : ‚Ñï ‚Üí TopCat.{u}
  /-- Each `sk (n+1)` (i.e., the `n`-skeleton) is obtained from `sk n` (i.e., the
  `(n-1)`-skeleton) by attaching `n`-disks. -/
  attach_cells (n : ‚Ñï) : RelativeCWComplex.AttachCells (n - 1) (sk n) (sk (n + 1))

/-- A CW-complex is a relative CW-complex whose `sk 0` (i.e., `(-1)`-skeleton) is empty. -/
structure CWComplex extends RelativeCWComplex.{u} where
  /-- `sk 0` (i.e., the `(-1)`-skeleton) is empty. -/
  isEmpty_sk_zero : IsEmpty (sk 0)

namespace RelativeCWComplex

noncomputable section Topology

/-- The inclusion map from `X` to `X'`, given that `X'` is obtained from `X` by attaching
`(n+1)`-disks -/
def AttachCells.inclusion (X X' : TopCat.{u}) (n : ‚Ñ§) (att : AttachCells n X X') : X ‚ü∂ X' :=
  @Limits.pushout.inr TopCat _ _ _ X (sigmaSphereInclusion n att.cells)
    (sigmaAttachMap n X att.cells att.attach_maps) _ ‚â´ att.iso_pushout.inv

/-- The inclusion map from `sk n` (i.e., the `(n-1)`-skeleton) to `sk (n+1)` (i.e., the
`n`-skeleton) of a relative CW-complex -/
def inclusion (X : RelativeCWComplex.{u}) (n : ‚Ñï) : X.sk n ‚ü∂ X.sk (n + 1) :=
  RelativeCWComplex.AttachCells.inclusion (X.sk n) (X.sk (n + 1)) (n - 1) (X.attach_cells n)

/-- The topology on a relative CW-complex -/
def toTopCat (X : RelativeCWComplex.{u}) : TopCat.{u} :=
  Limits.colimit <| Functor.ofSequence <| inclusion X

instance : Coe RelativeCWComplex TopCat where coe X := toTopCat X

end Topology

end RelativeCWComplex
