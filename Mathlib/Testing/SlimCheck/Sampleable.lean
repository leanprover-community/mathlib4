/-
Copyright (c) 2022 Henrik B√∂ving. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Henrik B√∂ving, Simon Hudon
-/
import Mathlib.Testing.SlimCheck.Gen
import Qq

#align_import testing.slim_check.sampleable from "leanprover-community/mathlib"@"fdc286cc6967a012f41b87f76dcd2797b53152af"

/-!
# `SampleableExt` Class

This class permits the creation samples of a given type
controlling the size of those values using the `Gen` monad.

# `Shrinkable` Class

This class helps minimize examples by creating smaller versions of
given values.

When testing a proposition like `‚àÄ n : ‚Ñï, prime n ‚Üí n ‚â§ 100`,
`SlimCheck` requires that `‚Ñï` have an instance of `SampleableExt` and for
`prime n` to be decidable.  `SlimCheck` will then use the instance of
`SampleableExt` to generate small examples of ‚Ñï and progressively increase
in size. For each example `n`, `prime n` is tested. If it is false,
the example will be rejected (not a test success nor a failure) and
`SlimCheck` will move on to other examples. If `prime n` is true,
`n ‚â§ 100` will be tested. If it is false, `n` is a counter-example of
`‚àÄ n : ‚Ñï, prime n ‚Üí n ‚â§ 100` and the test fails. If `n ‚â§ 100` is true,
the test passes and `SlimCheck` moves on to trying more examples.

This is a port of the Haskell QuickCheck library.

## Main definitions

* `SampleableExt` class
* `Shrinkable` class

### `SampleableExt`

`SampleableExt` can be used in two ways. The first (and most common)
is to simply generate values of a type directly using the `Gen` monad,
if this is what you want to do then `SampleableExt.mkSelfContained` is
the way to go.

Furthermore it makes it possible to express generators for types that
do not lend themselves to introspection, such as `‚Ñï ‚Üí ‚Ñï`.
If we test a quantification over functions the
counter-examples cannot be shrunken or printed meaningfully.
For that purpose, `SampleableExt` provides a proxy representation
`proxy` that can be printed and shrunken as well
as interpreted (using `interp`) as an object of the right type. If you
are using it in the first way, this proxy type will simply be the type
itself and the `interp` function `id`.

### `Shrinkable`

Given an example `x : Œ±`, `Shrinkable Œ±` gives us a way to shrink it
and suggest simpler examples.

## Shrinking

Shrinking happens when `SlimCheck` find a counter-example to a
property.  It is likely that the example will be more complicated than
necessary so `SlimCheck` proceeds to shrink it as much as
possible. Although equally valid, a smaller counter-example is easier
for a user to understand and use.

The `Shrinkable` class, , has a `shrink` function so that we can use
specialized knowledge while shrinking a value. It is not responsible
for the whole shrinking process however. It only has to take one step
in the shrinking process. `SlimCheck` will repeatedly call `shrink`
until no more steps can be taken. Because `shrink` guarantees that the
size of the candidates it produces is strictly smaller than the
argument, we know that `SlimCheck` is guaranteed to terminate.

## Tags

random testing

## References

* https://hackage.haskell.org/package/QuickCheck

-/

set_option autoImplicit true

namespace SlimCheck

open Random Gen

/-- Given an example `x : Œ±`, `Shrinkable Œ±` gives us a way to shrink it
and suggest simpler examples. -/
class Shrinkable (Œ± : Type u) where
  shrink : (x : Œ±) ‚Üí List Œ± := Œª _ => []

/-- `SampleableExt` can be used in two ways. The first (and most common)
is to simply generate values of a type directly using the `Gen` monad,
if this is what you want to do then `SampleableExt.mkSelfContained` is
the way to go.

Furthermore it makes it possible to express generators for types that
do not lend themselves to introspection, such as `‚Ñï ‚Üí ‚Ñï`.
If we test a quantification over functions the
counter-examples cannot be shrunken or printed meaningfully.
For that purpose, `SampleableExt` provides a proxy representation
`proxy` that can be printed and shrunken as well
as interpreted (using `interp`) as an object of the right type. -/
class SampleableExt (Œ± : Sort u) where
  proxy : Type v
  [proxyRepr : Repr proxy]
  [shrink : Shrinkable proxy]
  sample : Gen proxy
  interp : proxy ‚Üí Œ±

attribute [instance] SampleableExt.proxyRepr
attribute [instance] SampleableExt.shrink

namespace SampleableExt

/-- Use to generate instance whose purpose is to simply generate values
of a type directly using the `Gen` monad -/
def mkSelfContained [Repr Œ±] [Shrinkable Œ±] (sample : Gen Œ±) : SampleableExt Œ± where
  proxy := Œ±
  proxyRepr := inferInstance
  shrink := inferInstance
  sample := sample
  interp := id

/-- First samples a proxy value and interprets it. Especially useful if
the proxy and target type are the same. -/
def interpSample (Œ± : Type u) [SampleableExt Œ±] : Gen Œ± :=
  SampleableExt.interp <$> SampleableExt.sample

end SampleableExt

section Shrinkers

/-- `Nat.shrink' n` creates a list of smaller natural numbers by
successively dividing `n` by 2 . For example, `Nat.shrink 5 = [2, 1, 0]`. -/
def Nat.shrink (n : Nat) : List Nat :=
  if h : 0 < n then
    let m := n/2
    have : m < n := by
      apply Nat.div_lt_self h
      -- ‚ä¢ 1 < 2
      decide
      -- üéâ no goals
    m :: shrink m
  else
    []

instance Nat.shrinkable : Shrinkable Nat where
  shrink := Nat.shrink

instance Fin.shrinkable {n : Nat} : Shrinkable (Fin n.succ) where
  shrink m := Nat.shrink m

/-- `Int.shrinkable` operates like `Nat.shrinkable` but also includes the negative variants. -/
instance Int.shrinkable : Shrinkable Int where
  shrink n := Nat.shrink n.natAbs |>.map (Œª x => ([x, -x] : List ‚Ñ§)) |>.join

instance Bool.shrinkable : Shrinkable Bool := {}
instance Char.shrinkable : Shrinkable Char := {}

instance Prod.shrinkable [shrA : Shrinkable Œ±] [shrB : Shrinkable Œ≤] :
    Shrinkable (Prod Œ± Œ≤) where
  shrink := Œª (fst,snd) =>
    let shrink1 := shrA.shrink fst |>.map fun x ‚Ü¶ (x, snd)
    let shrink2 := shrB.shrink snd |>.map fun x ‚Ü¶ (fst, x)
    shrink1 ++ shrink2

instance Sigma.shrinkable [shrA : Shrinkable Œ±] [shrB : Shrinkable Œ≤] :
    Shrinkable ((_ : Œ±) √ó Œ≤) where
  shrink := Œª ‚ü®fst,snd‚ü© =>
    let shrink1 := shrA.shrink fst |>.map fun x ‚Ü¶ ‚ü®x, snd‚ü©
    let shrink2 := shrB.shrink snd |>.map fun x ‚Ü¶ ‚ü®fst, x‚ü©
    shrink1 ++ shrink2

open Shrinkable

/-- Shrink a list of a shrinkable type, either by discarding an element or shrinking an element. -/
instance List.shrinkable [Shrinkable Œ±] : Shrinkable (List Œ±) where
  shrink := fun L =>
    (L.mapIdx fun i _ => L.removeNth i) ++
    (L.mapIdx fun i a => (shrink a).map fun a' => L.modifyNth (fun _ => a') i).join

end Shrinkers

section Samplers

open SampleableExt

instance Nat.sampleableExt : SampleableExt Nat :=
  mkSelfContained (do choose Nat 0 (‚ÜêgetSize) (Nat.zero_le _))

instance Fin.sampleableExt {n : Nat} : SampleableExt (Fin (n.succ)) :=
  mkSelfContained (do choose (Fin n.succ) (Fin.ofNat 0) (Fin.ofNat (‚ÜêgetSize)) (by
    simp [Fin.ofNat, LE.le]
    -- ‚ä¢ 0 ‚â§ __do_lift‚úù % (n + 1)
    exact Nat.zero_le _
    -- üéâ no goals
  ))

instance Int.sampleableExt : SampleableExt Int :=
  mkSelfContained (do
    choose Int (-(‚ÜêgetSize)) (‚ÜêgetSize)
      (le_trans (Int.neg_nonpos_of_nonneg (Int.ofNat_zero_le _)) (Int.ofNat_zero_le _)))

instance Bool.sampleableExt : SampleableExt Bool :=
  mkSelfContained $ chooseAny Bool

/-- This can be specialized into customized `SampleableExt Char` instances.
The resulting instance has `1 / length` chances of making an unrestricted choice of characters
and it otherwise chooses a character from `chars` with uniform probabilities.  -/
def Char.sampleable (length : Nat) (chars : List Char) (pos : 0 < chars.length) :
    SampleableExt Char :=
  mkSelfContained do
    let x ‚Üê choose Nat 0 length (Nat.zero_le _)
    if x.val == 0 then
      let n ‚Üê interpSample Nat
      pure $ Char.ofNat n
    else
      elements chars pos

instance Char.sampleableDefault : SampleableExt Char :=
  Char.sampleable 3 " 0123abcABC:,;`\\/".toList (by decide)
                                                    -- üéâ no goals

instance Prod.sampleableExt {Œ± : Type u} {Œ≤ : Type v} [SampleableExt Œ±] [SampleableExt Œ≤] :
    SampleableExt (Œ± √ó Œ≤) where
  proxy := Prod (proxy Œ±) (proxy Œ≤)
  proxyRepr := inferInstance
  shrink := inferInstance
  sample := prodOf sample sample
  interp := Prod.map interp interp

instance Prop.sampleableExt : SampleableExt Prop where
  proxy := Bool
  proxyRepr := inferInstance
  sample := interpSample Bool
  shrink := inferInstance
  interp := Coe.coe

instance List.sampleableExt [SampleableExt Œ±] : SampleableExt (List Œ±) where
  proxy := List (proxy Œ±)
  sample := Gen.listOf sample
  interp := List.map interp

end Samplers

/-- An annotation for values that should never get shrinked. -/
def NoShrink (Œ± : Type u) := Œ±

namespace NoShrink

def mk (x : Œ±) : NoShrink Œ± := x
def get (x : NoShrink Œ±) : Œ± := x

instance inhabited [inst : Inhabited Œ±] : Inhabited (NoShrink Œ±) := inst
instance repr [inst : Repr Œ±] : Repr (NoShrink Œ±) := inst

instance shrinkable : Shrinkable (NoShrink Œ±) where
  shrink := Œª _ => []

instance sampleableExt [SampleableExt Œ±] [Repr Œ±] : SampleableExt (NoShrink Œ±) :=
  SampleableExt.mkSelfContained $ (NoShrink.mk ‚àò SampleableExt.interp) <$> SampleableExt.sample

end NoShrink


/--
Print (at most) 10 samples of a given type to stdout for debugging.
-/
-- Porting note: if `Control.ULiftable` is ported, make use of that here, as in mathlib3,
-- to enable sampling from higher types.
def printSamples {t : Type} [Repr t] (g : Gen t) : IO PUnit := do
  for i in List.range 10 do
    IO.println s!"{repr (‚Üê g.run i)}"

open Lean Meta Qq

/-- Create a `Gen Œ±` expression from the argument of `#sample` -/
def mkGenerator (e : Expr) : MetaM (Expr √ó Expr) := do
  let t ‚Üê inferType e
  match t.getAppFnArgs with
  | (`Gen, #[t]) => do
    let repr_inst ‚Üê synthInstance (‚Üê mkAppM ``Repr #[t])
    pure (repr_inst, e)
  | _ => do
    let sampleableExt_inst ‚Üê synthInstance (mkApp (‚Üê mkConstWithFreshMVarLevels ``SampleableExt) e)
    let repr_inst ‚Üê mkAppOptM ``SampleableExt.proxyRepr #[e, sampleableExt_inst]
    let gen ‚Üê mkAppOptM ``SampleableExt.sample #[none, sampleableExt_inst]
    pure (repr_inst, gen)

open Elab

/--
`#sample type`, where `type` has an instance of `SampleableExt`, prints ten random
values of type `type` using an increasing size parameter.

```lean
#sample Nat
-- prints
-- 0
-- 0
-- 2
-- 24
-- 64
-- 76
-- 5
-- 132
-- 8
-- 449
-- or some other sequence of numbers

#sample List Int
-- prints
-- []
-- [1, 1]
-- [-7, 9, -6]
-- [36]
-- [-500, 105, 260]
-- [-290]
-- [17, 156]
-- [-2364, -7599, 661, -2411, -3576, 5517, -3823, -968]
-- [-643]
-- [11892, 16329, -15095, -15461]
-- or whatever
```
-/
elab "#sample " e:term : command =>
  Command.runTermElabM fun _ => do
    let e ‚Üê Elab.Term.elabTermAndSynthesize e none
    let (repr_inst, gen) ‚Üê mkGenerator e
    let printSamples ‚Üê mkAppOptM ``printSamples #[none, repr_inst, gen]
    let code ‚Üê unsafe evalExpr (IO PUnit) q(IO PUnit) printSamples
    _ ‚Üê code

end SlimCheck
