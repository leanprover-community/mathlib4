/-
Copyright (c) 2021 Henrik B√∂ving. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Henrik B√∂ving, Simon Hudon
-/
import Mathlib.Control.Random
import Mathlib.Control.ULiftable
import Mathlib.Data.List.Perm
import Mathlib.Data.Subtype
import Mathlib.Data.Nat.Basic

#align_import testing.slim_check.gen from "leanprover-community/mathlib"@"fdc286cc6967a012f41b87f76dcd2797b53152af"

/-!
# `Gen` Monad

This monad is used to formulate randomized computations with a parameter
to specify the desired size of the result.
This is a port of the Haskell QuickCheck library.

## Main definitions

* `Gen` monad

## Tags

random testing

## References

* https://hackage.haskell.org/package/QuickCheck
-/

set_option autoImplicit true

namespace SlimCheck

open Random

/-- Monad to generate random examples to test properties with.
It has a `Nat` parameter so that the caller can decide on the
size of the examples. -/
abbrev Gen (Œ± : Type u) := ReaderT (ULift Nat) Rand Œ±

namespace Gen

/-- Lift `Random.random` to the `Gen` monad. -/
def chooseAny (Œ± : Type u) [Random Œ±] : Gen Œ± :=
  Œª _ => rand Œ±

/-- Lift `BoundedRandom.randomR` to the `Gen` monad. -/
def choose (Œ± : Type u) [Preorder Œ±] [BoundedRandom Œ±] (lo hi : Œ±) (h : lo ‚â§ hi) :
    Gen {a // lo ‚â§ a ‚àß a ‚â§ hi} :=
  Œª _ => randBound Œ± lo hi h

lemma chooseNatLt_aux {lo hi : Nat} (a : Nat) (h : Nat.succ lo ‚â§ a ‚àß a ‚â§ hi) :
    lo ‚â§ Nat.pred a ‚àß Nat.pred a < hi :=
  And.intro (Nat.le_pred_of_lt (Nat.lt_of_succ_le h.left)) <|
    show a.pred.succ ‚â§ hi by
       rw [Nat.succ_pred_eq_of_pos]
       -- ‚ä¢ a ‚â§ hi
       exact h.right
       -- ‚ä¢ 0 < a
       exact lt_of_le_of_lt (Nat.zero_le lo) h.left
       -- üéâ no goals

/-- Generate a `Nat` example between `x` and `y` (exclusively). -/
def chooseNatLt (lo hi : Nat) (h : lo < hi) : Gen {a // lo ‚â§ a ‚àß a < hi} :=
  Subtype.map Nat.pred chooseNatLt_aux <$> choose Nat (lo.succ) hi (Nat.succ_le_of_lt h)

/-- Get access to the size parameter of the `Gen` monad. -/
def getSize : Gen Nat :=
  return (‚Üê read).down

/-- Apply a function to the size parameter. -/
def resize (f : Nat ‚Üí Nat) (x : Gen Œ±) : Gen Œ± :=
  withReader (ULift.up ‚àò f ‚àò ULift.down) x

variable {Œ± : Type u}

/-- Create an `Array` of examples using `x`. The size is controlled
by the size parameter of `Gen`. -/
def arrayOf (x : Gen Œ±) : Gen (Array Œ±) := do
  let ‚ü®sz‚ü© ‚Üê (ULiftable.up <| do choose Nat 0 (‚ÜêgetSize) (Nat.zero_le _) : Gen (ULift ‚Ñï))
  let mut res := #[]
  for _ in [0:sz] do
    res := res.push (‚Üê x)
  pure res

/-- Create a `List` of examples using `x`. The size is controlled
by the size parameter of `Gen`. -/
def listOf (x : Gen Œ±) : Gen (List Œ±) :=
  arrayOf x >>= pure ‚àò Array.toList

/-- Given a list of example generators, choose one to create an example. -/
def oneOf (xs : Array (Gen Œ±)) (pos : 0 < xs.size := by decide) : Gen Œ± := do
  let ‚ü®x, _, h2‚ü© ‚Üê ULiftable.up <| chooseNatLt 0 xs.size pos
  xs.get ‚ü®x, h2‚ü©

/-- Given a list of examples, choose one to create an example. -/
def elements (xs : List Œ±) (pos : 0 < xs.length) : Gen Œ± := do
  let ‚ü®x, _, h2‚ü© ‚Üê ULiftable.up <| chooseNatLt 0 xs.length pos
  pure $ xs.get ‚ü®x, h2‚ü©

open List in
/-- Generate a random permutation of a given list. -/
def permutationOf : (xs : List Œ±) ‚Üí Gen { ys // xs ~ ys }
  | [] => pure ‚ü®[], Perm.nil‚ü©
  | x::xs => do
    let ‚ü®ys, h1‚ü© ‚Üê permutationOf xs
    let ‚ü®n, _, h3‚ü© ‚Üê ULiftable.up <| choose Nat 0 ys.length (Nat.zero_le _)
    pure ‚ü®insertNth n x ys, Perm.trans (Perm.cons _ h1) (perm_insertNth _ _ h3).symm‚ü©

/-- Given two generators produces a tuple consisting out of the result of both -/
def prodOf {Œ± : Type u} {Œ≤ : Type v} (x : Gen Œ±) (y : Gen Œ≤) : Gen (Œ± √ó Œ≤) := do
  let ‚ü®a‚ü© ‚Üê (ULiftable.up x : Gen (ULift.{max u v} Œ±))
  let ‚ü®b‚ü© ‚Üê (ULiftable.up y : Gen (ULift.{max u v} Œ≤))
  pure (a, b)

end Gen

/-- Execute a `Gen` inside the `IO` monad using `size` as the example size-/
def Gen.run (x : Gen Œ±) (size : Nat) : BaseIO Œ± :=
  IO.runRand $ ReaderT.run x ‚ü®size‚ü©


end SlimCheck
