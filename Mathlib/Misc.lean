import Mathlib.Data.Int.Star
import Mathlib.RingTheory.DedekindDomain.Dvr
import Mathlib.RingTheory.Polynomial.Cyclotomic.Roots
import Mathlib.NumberTheory.NumberField.Ideal.KummerDedekind
import Mathlib.NumberTheory.Cyclotomic.Rat
import Mathlib.Algebra.Group.NatPowAssoc
import Mathlib.Riccardo
import Mathlib.Algebra.CharP.Two

theorem Nat.eq_or_eq_of_totient_eq_totient {a b : ‚Ñï} (h : a ‚à£ b) (h' : a.totient = b.totient) :
    a = b ‚à® 2 * a = b := by
  by_cases ha : a = 0
  ¬∑ rw [ha, totient_zero, eq_comm, totient_eq_zero] at h'
    simp [ha, h']
  by_cases hb : b = 0
  ¬∑ rw [hb, totient_zero, totient_eq_zero] at h'
    exact False.elim (ha h')
  obtain ‚ü®c, rfl‚ü© := h
  suffices a.Coprime c by
    rw [totient_mul this, eq_comm, mul_eq_left (totient_eq_zero.not.mpr ha),
      totient_eq_one_iff] at h'
    obtain rfl | rfl := h'
    ¬∑ simp
    ¬∑ simp [mul_comm]
  refine coprime_of_dvd fun p hp hap ‚Ü¶ ?_
  rintro ‚ü®d, rfl‚ü©
  suffices a.totient < (p * a * d).totient by
    rw [‚Üê mul_assoc, mul_comm a] at h'
    exact h'.not_lt this
  rw [mul_comm p]
  refine lt_of_lt_of_le ?_ (Nat.le_of_dvd ?_ (totient_dvd_of_dvd ‚ü®d, rfl‚ü©))
  ¬∑ rw [mul_comm, totient_mul_of_prime_of_dvd hp hap, Nat.lt_mul_iff_one_lt_left]
    ¬∑ exact hp.one_lt
    ¬∑ exact totient_pos.mpr <| pos_of_ne_zero ha
  ¬∑ exact totient_pos.mpr <| zero_lt_of_ne_zero (by rwa [mul_assoc])

theorem Nat.eq_of_totient_eq_totient {a b : ‚Ñï} (h : a ‚à£ b) (ha : Even a)
    (h' : a.totient = b.totient) : a = b := by
  by_cases ha' : a = 0
  ¬∑ rw [ha', totient_zero, eq_comm, totient_eq_zero] at h'
    rw [h', ha']
  refine (eq_or_eq_of_totient_eq_totient h h').resolve_right fun h ‚Ü¶ ?_
  rw [‚Üê h, totient_mul_of_prime_of_dvd (prime_two) (even_iff_two_dvd.mp ha), eq_comm,
    mul_eq_right (totient_eq_zero.not.mpr ha')] at h'
  cutsat

theorem ZMod.orderOf_mod_self_pow_sub_one {n k : ‚Ñï} (hn : 1 < n) (hk : 0 < k) :
    orderOf (n : ZMod (n ^ k - 1)) = k := by
  have : NeZero n := NeZero.of_gt hn
  refine (orderOf_eq_iff hk).mpr ‚ü®?_, fun m hm‚ÇÅ hm‚ÇÇ ‚Ü¶ ?_‚ü©
  ¬∑ rw [‚Üê Nat.cast_npow, ‚Üê sub_eq_zero, ‚Üê Nat.cast_one (R := ZMod (n ^ k - 1)),
      ‚Üê Nat.cast_sub NeZero.one_le]
    exact ZMod.natCast_self _
  ¬∑ rw [ne_eq, ‚Üê Nat.cast_npow, ‚Üê sub_eq_zero, ‚Üê Nat.cast_one (R := ZMod (n ^ k - 1)),
      ‚Üê Nat.cast_sub NeZero.one_le, ZMod.natCast_eq_zero_iff]
    refine (Nat.le_of_dvd ?_).mt (not_le.mpr ?_)
    ¬∑ exact Nat.zero_lt_sub_of_lt <| Nat.one_lt_pow hm‚ÇÇ.ne' hn
    ¬∑ exact Nat.sub_lt_sub_iff_right NeZero.one_le (c := 1).mpr <| Nat.pow_lt_pow_right hn hm‚ÇÅ

theorem mem_torsion_iff_isPrimitiveRoot {G : Type*} [CommGroup G] {Œ∂ : G} :
    Œ∂ ‚àà CommGroup.torsion G ‚Üî (‚àÉ k, k ‚â† 0 ‚àß IsPrimitiveRoot Œ∂ k) := by
  rw [CommGroup.mem_torsion, isOfFinOrder_iff_pow_eq_one]
  refine ‚ü®fun ‚ü®n, hn‚ÇÅ, hn‚ÇÇ‚ü© ‚Ü¶ ?_, fun ‚ü®k, hk‚ÇÅ, hk‚ÇÇ‚ü© ‚Ü¶ ?_‚ü©
  ¬∑ exact ‚ü®orderOf Œ∂, orderOf_ne_zero_iff.mpr ‚ü®n, hn‚ÇÅ, (isPeriodicPt_mul_iff_pow_eq_one _).mpr hn‚ÇÇ‚ü©,
      IsPrimitiveRoot.orderOf Œ∂‚ü©
  ¬∑ exact ‚ü®k, Nat.zero_lt_of_ne_zero hk‚ÇÅ, hk‚ÇÇ.pow_eq_one‚ü©

theorem CommGroup.mem_torsion_of_isPrimitiveRoot (k : ‚Ñï) [NeZero k] {G : Type*} [CommGroup G]
    {Œ∂ : G} (hŒ∂ : IsPrimitiveRoot Œ∂ k) :
    Œ∂ ‚àà CommGroup.torsion G :=
  mem_torsion_iff_isPrimitiveRoot.mpr ‚ü®k, NeZero.ne _, hŒ∂‚ü©

@[simp]
lemma RingHom.rangeRestrict_injective_iff {R S : Type*} [Ring R] [Ring S] {f : R ‚Üí+* S} :
    Function.Injective f.rangeRestrict ‚Üî Function.Injective f := by
  convert Set.injective_codRestrict _

@[to_additive]
theorem MonoidAlgebra.single_sub {R M : Type*} [Ring R] (a : M) (b‚ÇÅ b‚ÇÇ : R) :
    single a (b‚ÇÅ - b‚ÇÇ) = single a b‚ÇÅ - single a b‚ÇÇ :=
  Finsupp.single_sub _ _ _

@[to_additive (attr := simp)]
theorem MonoidAlgebra.fintype_sum_single {k G : Type*} [Fintype G] [Semiring k]
    (f : MonoidAlgebra k G) : ‚àë g : G, single g (f g) = f := by
  classical
  rw [‚Üê sum_single f, Finsupp.sum_fintype]
  ¬∑ conv_lhs =>
      enter [2, g, 2]
      rw [Finset.sum_apply']
      simp [single_apply]
  ¬∑ intro _
    simp

theorem IsCyclotomicExtension.union_of_isPrimitiveRoot (S : Set ‚Ñï) (A B : Type*) [CommRing A]
    [CommRing B] [Algebra A B] [hB : IsCyclotomicExtension S A B] {n : ‚Ñï} {r : B}
    (hr : IsPrimitiveRoot r n) :
    IsCyclotomicExtension (S ‚à™ {n}) A B := by
  by_cases hn : n = 0
  ¬∑ rwa [hn, eq_self_sdiff_zero, Set.union_diff_right, ‚Üê eq_self_sdiff_zero]
  rw [iff_adjoin_eq_top]
  refine ‚ü®fun m hm‚ÇÅ hm‚ÇÇ ‚Ü¶ ?_, le_antisymm (by aesop) ?_‚ü©
  ¬∑ obtain hm‚ÇÅ | rfl := hm‚ÇÅ
    ¬∑ exact exists_isPrimitiveRoot A B hm‚ÇÅ hm‚ÇÇ
    ¬∑ use r
  ¬∑ rw [‚Üê ((iff_adjoin_eq_top _ _ _).mp hB).2]
    exact Algebra.adjoin_mono (by aesop)

-- lifted from #29517

lemma IsPrimitiveRoot.div_of_dvd {M : Type*} [CommMonoid M] {Œ∂ : M} {n m : ‚Ñï} [NeZero n]
    (hŒ∂ : IsPrimitiveRoot Œ∂ n) (h : m ‚à£ n) :
    IsPrimitiveRoot (Œ∂ ^ (n / m)) m := by
  have hm0 : 0 < m := by
    rw [Nat.pos_iff_ne_zero]
    rintro rfl
    simp only [zero_dvd_iff] at h
    exact NeZero.out h
  obtain ‚ü®k, rfl‚ü© := id h
  have hk0 : 0 < k := by
    rw [Nat.pos_iff_ne_zero]
    rintro rfl
    simp_all
  simpa [hm0, hk0] using hŒ∂.pow_of_dvd hk0.ne' (dvd_mul_left _ _)

-- These should be generalized

open NumberField in
theorem NumberField.Units.rootsOfUnity_eq_rootsOfUnity (K : Type*) [Field K] [NumberField K]
    (n : ‚Ñï) [NeZero n] :
    rootsOfUnity n (ùìû K) = rootsOfUnity (n.gcd (torsionOrder K)) (ùìû K) := by
  ext Œ∂
  rw [mem_rootsOfUnity, mem_rootsOfUnity]
  refine ‚ü®fun h ‚Ü¶ pow_gcd_eq_one Œ∂ h ?_, fun h ‚Ü¶ ?_‚ü©
  ¬∑ have : Œ∂ ‚àà torsion K := by
      rw [torsion, CommGroup.mem_torsion, isOfFinOrder_iff_pow_eq_one]
      exact ‚ü®n, NeZero.pos n, h‚ü©
    rwa [‚Üê rootsOfUnity_eq_torsion] at this
  ¬∑ obtain ‚ü®d, hd‚ü© := Nat.gcd_dvd_left n (torsionOrder K)
    rw [hd, pow_mul, h, one_pow]

open NumberField in
theorem NumberField.Units.card_rootsOfUnity (K : Type*) [Field K] [NumberField K]
    (n : ‚Ñï) [NeZero n] (hn : n ‚à£ torsionOrder K) :
    Fintype.card (rootsOfUnity n (ùìû K)) = n := by
  obtain ‚ü®g, hg‚ü© : ‚àÉ g : ùìû K, IsPrimitiveRoot g (torsionOrder K) := by
    rw [‚Üê card_rootsOfUnity_eq_iff_exists_isPrimitiveRoot]
    simp_rw [rootsOfUnity_eq_torsion, torsionOrder]
  exact IsPrimitiveRoot.card_rootsOfUnity (hg.div_of_dvd hn)
