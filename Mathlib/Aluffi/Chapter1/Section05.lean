import Mathlib.CategoryTheory.Category.Pointed
import Mathlib.CategoryTheory.Types
import Mathlib.CategoryTheory.Opposites
import Mathlib.Data.Int.GCD

set_option autoImplicit false

universe u v w

open CategoryTheory Opposite

-- 5.1. Prove that a final object in a category C is initial in the opposite category Cáµ’áµ–
-- Setting up my own API because I don't understand the mathlib IsInitial yet (what are cocones?)
def Initial {C : Type _} [Category C] (I : C) :=
    âˆ€ A : C, Unique (I âŸ¶ A)

instance {C : Type _} [Category C] (I : C) : Subsingleton (Initial I) where
  allEq := by
    intros
    funext
    simp

def Final {C : Type _} [Category C] (F : C) :=
    âˆ€ A : C, Unique (A âŸ¶ F)

instance {C : Type _} [Category C] (F : C) : Subsingleton (Final F) where
  allEq := by
    intros
    funext
    simp

nonrec
def Final.op {C : Type _} [Category C] {F : C} (hF : Final F) : Initial (op F) :=
    Î» A => âŸ¨âŸ¨op (hF (unop A)).defaultâŸ©, Î» _ => unop_injective ((hF (unop A)).uniq _)âŸ©

-- 5.2. Prove that 0 is the unique initial object in Set. [Â§5.1]
def PEmpty_Initial : Initial PEmpty :=
  Î» _ => âŸ¨âŸ¨Î» x => x.elimâŸ©, Î» _ => funext (Î» x => x.elim)âŸ©

-- universes needed so that universes aren't constrained unnecessarily and proofs can't be found
-- cf. https://leanprover.zulipchat.com/#narrow/stream/270676-lean4/topic/Failure.20of.20TC.20search.20in.20.60simp.60.20with.20.60etaExperiment.60.2E
def Initial.Iso {C : Type _} [Category.{u, v} C] {X Y : C} (hX : Initial X) (hY : Initial Y) : X â‰… Y where
  hom := (hX Y).default
  inv := (hY X).default
  hom_inv_id := by have := hX X; simp
  inv_hom_id := by have := hY Y; simp

def Iso.Equiv {X Y : Type _} (f : X â‰… Y) : X â‰ƒ Y where
  toFun := f.hom
  invFun := f.inv
  left_inv := congrFun f.hom_inv_id
  right_inv := congrFun f.inv_hom_id

-- can't prove uniqueness in Lean because no type extensionality
-- because I can define my own empty inductive `MyEmpty`

def Initial.IsoPEmpty {X : Type _} (hX : Initial X) : X â‰… PEmpty := hX.Iso PEmpty_Initial

-- 5.3. Prove that final objects are unique up to isomorphism. [Â§5.1]
def Final.Iso {C : Type _} [Category.{u, v} C] {X Y : C} (hX : Final X) (hY : Final Y) : X â‰… Y where
  hom := (hY X).default
  inv := (hX Y).default
  hom_inv_id := by have := hX X; simp
  inv_hom_id := by have := hY Y; simp

-- 5.4. What are initial and final objects in the category of `pointed sets' (Example 3.8)?
-- Are they unique?
def PointedPunit_Initial : Initial (Pointed.of PUnit.unit) :=
    Î» A => âŸ¨âŸ¨âŸ¨Î» _ => A.point, rflâŸ©âŸ©, by
      intro f
      refine' Pointed.Hom.ext _ _ _
      funext
      exact f.map_pointâŸ©

def PointedPunit_Final : Final (Pointed.of PUnit.unit) :=
    Î» A => âŸ¨âŸ¨âŸ¨Î» _ => PUnit.unit, rflâŸ©âŸ©, by
      intro f
      refine' Pointed.Hom.ext _ _ _
      funext
      exact f.map_pointâŸ©
-- not unique, any singleton set is isomorphic to PUnit

-- 5.5. What are the final objects in the category considered in Â§5.3? [Â§5.3]
-- Section 5.3: "The quotient A/~ is universal with respect to the property of mapping A to a
-- set in such a way that equivalent elements have the same image."
variable {A : Type _}

instance section53 (r : A â†’ A â†’ Prop) :
    Category (Î£' Z : Type _, {f : A â†’ Z // âˆ€ â¦ƒa b : Aâ¦„, r a b â†’ f a = f b}) where
  Hom := Î» âŸ¨Z, fâŸ© âŸ¨Z', gâŸ© => {Ïƒ : Z â†’ Z' // Ïƒ âˆ˜ f.1 = g.1}
  id _ := âŸ¨id, Function.comp.left_id _âŸ©
  comp f g := âŸ¨g.1 âˆ˜ f.1, by rw [Function.comp.assoc, f.2, g.2]âŸ©
  id_comp _ := Subtype.ext (Function.comp.left_id _)
  comp_id _ := Subtype.ext (Function.comp.right_id _)
  assoc _ _ _ := Subtype.ext (Function.comp.assoc _ _ _)

def Quot_Initial (r : A â†’ A â†’ Prop) :
    Initial (âŸ¨_, Quot.mk r, Î» _ _ h => Quot.sound hâŸ© :
      (Î£' Z : Type _, {f : A â†’ Z // âˆ€ â¦ƒa b : Aâ¦„, r a b â†’ f a = f b})) :=
  Î» âŸ¨Z, fâŸ© => âŸ¨âŸ¨âŸ¨Quot.lift f.1 f.2, funext Î» x => Quot.lift_mk f.1 f.2 xâŸ©âŸ©,
    Î» âŸ¨f', hfâŸ© => by
      refine' Subtype.ext _
      ext âŸ¨xâŸ©
      dsimp
      simp_rw [â†hf]
      rflâŸ©

def PUnit_Final (r : A â†’ A â†’ Prop) :
    Final (âŸ¨PUnit, Î» _ => PUnit.unit, Î» _ _ _ => rflâŸ© :
      (Î£' Z : Type _, {f : A â†’ Z // âˆ€ â¦ƒa b : Aâ¦„, r a b â†’ f a = f b})) :=
  Î» _ => âŸ¨âŸ¨âŸ¨Î» _ => PUnit.unit, funext Î» _ => rflâŸ©âŸ©, Î» _ => rflâŸ©

-- 5.6. Consider the category corresponding to endowing (as in Example 3.3) theset Z+ of
-- positive integers with the divisibility relation. Thus there is exactly one morphism d -> m in
-- this category if and only if d divides m without remainder; there is no morphism between d and m
-- otherwise. Show that this category has products and coproducts.
-- What are their `conventional' names? [Â§VII.5.1]

-- Redefining products and coproducts because I don't yet understand Limits and functors

instance example39_down {C : Type _} [hC : Category C] (A B : C) :
    Category (Î£ (Z : C), hC.Hom Z A Ã— hC.Hom Z B) where
  Hom := Î» âŸ¨Zâ‚, fâ‚âŸ© âŸ¨Zâ‚‚, fâ‚‚âŸ© => {Ïƒ : Zâ‚ âŸ¶ Zâ‚‚ // Ïƒ â‰« fâ‚‚.fst = fâ‚.fst âˆ§ Ïƒ â‰« fâ‚‚.snd = fâ‚.snd}
  id  _ := âŸ¨ğŸ™ _, hC.id_comp _, hC.id_comp _âŸ©
  comp := Î» âŸ¨f, hfâŸ© âŸ¨g, hgâŸ© => âŸ¨f â‰« g,
    by rw [hC.assoc, hg.left, hf.left],
    by rw [hC.assoc, hg.right, hf.right]âŸ©
  id_comp := by
    intros
    exact Subtype.ext (hC.id_comp _)
  comp_id := by
    intros
    exact Subtype.ext (hC.comp_id _)
  assoc := by
    intros
    exact Subtype.ext (hC.assoc _ _ _)

instance example39_up {C : Type _} [hC : Category C] (A B : C) :
    Category (Î£ (Z : C), hC.Hom A Z Ã— hC.Hom B Z) where
  Hom := Î» âŸ¨Zâ‚, fâ‚âŸ© âŸ¨Zâ‚‚, fâ‚‚âŸ© => {Ïƒ : Zâ‚ âŸ¶ Zâ‚‚ // fâ‚.fst â‰« Ïƒ = fâ‚‚.fst âˆ§ fâ‚.snd â‰« Ïƒ = fâ‚‚.snd}
  id  _ := âŸ¨ğŸ™ _, hC.comp_id _, hC.comp_id _âŸ©
  comp := Î» âŸ¨f, hfâŸ© âŸ¨g, hgâŸ© => âŸ¨f â‰« g,
    by rw [â†hC.assoc, â†hg.left, â†hf.left],
    by rw [â†hC.assoc, â†hg.right, â†hf.right]âŸ©
  id_comp := by
    intros
    exact Subtype.ext (hC.id_comp _)
  comp_id := by
    intros
    exact Subtype.ext (hC.comp_id _)
  assoc := by
    intros
    exact Subtype.ext (hC.assoc _ _ _)

class HasFiniteProducts (C : Type _) [hC : Category C] : Prop where
  hasFinal : âˆ€ A B : C, âˆƒ F : (Î£ (Z : C), hC.Hom Z A Ã— hC.Hom Z B), Nonempty (Final F)

class HasFiniteCoproducts (C : Type _) [hC : Category C] : Prop where
  hasInitial : âˆ€ A B : C, âˆƒ I : (Î£ (Z : C), hC.Hom A Z Ã— hC.Hom B Z), Nonempty (Initial I)

namespace exercise56
inductive obj | of : (d : â„¤) â†’ obj

def obj.un : obj â†’ â„¤ | of d => d

inductive hom (d m : obj)
| of : d.un âˆ£ m.un â†’ hom d m

def hom.un {d m : obj} : hom d m â†’ d.un âˆ£ m.un | of h => h
def hom.ext {d m : obj} (h h' : d.un âˆ£ m.un) : hom.of h = hom.of h' := rfl

instance : Category obj where
  Hom := hom
  id _ := .of (Int.dvd_refl _)
  comp f g := .of (Int.dvd_trans f.un g.un)
  id_comp _ := rfl
  comp_id _ := rfl
  assoc _ _ _ := rfl

instance : HasFiniteProducts obj where
  hasFinal d m := âŸ¨
    âŸ¨.of (Int.gcd d.un m.un), .of (Int.gcd_dvd_left _ _), .of (Int.gcd_dvd_right _ _)âŸ©,
    âŸ¨Î» âŸ¨_, hkd, hkmâŸ© => âŸ¨âŸ¨.of (Int.dvd_gcd hkd.un hkm.un), rfl, rflâŸ©, Î» _ => rflâŸ©âŸ©âŸ©

instance : HasFiniteCoproducts obj where
  hasInitial d m := âŸ¨
    âŸ¨.of (Int.lcm d.un m.un), .of (Int.dvd_lcm_left _ _), .of (Int.dvd_lcm_right _ _)âŸ©,
    âŸ¨Î» âŸ¨_, hkd, hkmâŸ© => âŸ¨âŸ¨.of (Int.lcm_dvd hkd.un hkm.un), rfl, rflâŸ©, Î» _ => rflâŸ©âŸ©âŸ©

end exercise56

-- 5.7. Redo Exercise 2.9, this time using Proposition 5.4.
-- 2.9
-- Show that if A' â‰… A" and B' â‰… B" and further A' âˆ© B' = 0 and A" âˆ© B" = 0, then A' âˆª B' â‰… A" âˆª B".
-- Conclude that the operation A â¨† B (as described in Â§1.4) is well-defined up to isomorphism
-- (cf. Â§2.9). [Â§2.9, 5.7]

def Initial.Iso_unique {C : Type _} [Category.{u, v} C] {X Y : C} (hX : Initial X) (hY : Initial Y) :
    Unique (X â‰… Y) where
  default := hX.Iso hY
  uniq e := Iso.ext $ by
    have : Unique (X âŸ¶ Y) := hX Y
    exact Subsingleton.elim _ _

def Final.Iso_unique {C : Type _} [Category.{u, v} C] {X Y : C} (hX : Final X) (hY : Final Y) :
    Unique (X â‰… Y) where
  default := hX.Iso hY
  uniq e := Iso.ext $ by
    have : Unique (X âŸ¶ Y) := hY X
    exact Subsingleton.elim _ _

instance category57 {X : Type _} : Category (Set X) where
  Hom s t := s â†’ t
  id s := id
  comp f g := g âˆ˜ f

noncomputable
def category57.Initial_Union {X : Type _} (A B : Set X) (hAB : A âˆ© B = âˆ…) :
  @Initial (Î£ (Z : Set X), (A âŸ¶ Z) Ã— (B âŸ¶ Z)) _ âŸ¨A âˆª B, Î» x => âŸ¨x, by simpâŸ©, Î» x => âŸ¨x, by simpâŸ©âŸ© :=
  Î» âŸ¨Z, f, gâŸ© => by
    classical
    exact {
      default := âŸ¨Î» x => if hx : (x : X) âˆˆ A then f âŸ¨x, hxâŸ© else g âŸ¨x, by simpa [hx] using x.propâŸ©,
        funext <| Î» x => by
        change (_ âˆ˜ _) x = f x
        simp [x.prop], funext <| Î» x => by
        change (_ âˆ˜ _) x = g x
        have hx : (x : X) âˆ‰ A := Î» H => by simpa using hAB.le (Set.mem_inter H x.prop)
        simp [x.prop, hx]âŸ©
      uniq := Î» h => by
        refine' Subtype.ext _
        funext x
        dsimp
        cases' x.prop with hx hx
        Â· simpa [hx] using congrFun h.2.1 âŸ¨x, hxâŸ©
        Â· have hx' : (x : X) âˆ‰ A := Î» H => by simpa using hAB.le (Set.mem_inter H hx)
          simpa [hx, hx'] using congrFun h.2.2 âŸ¨x, hxâŸ© }

noncomputable
def category57.Initial_Union' {X : Type _} (A B A' B' : Set X)
    (_ : A âˆ© B = âˆ…) (hAB' : A' âˆ© B' = âˆ…) (e : A â‰… A') (e' : B â‰… B') :
  @Initial (Î£ (Z : Set X), (A âŸ¶ Z) Ã— (B âŸ¶ Z)) _ âŸ¨A' âˆª B', e.hom â‰« (Î» x => âŸ¨x, by simpâŸ©), e'.hom â‰« (Î» x => âŸ¨x, by simpâŸ©)âŸ© :=
  Î» âŸ¨Z, f, gâŸ© => by
    classical
    exact {
      default := âŸ¨Î» x => if hx : (x : X) âˆˆ A' then f (e.inv âŸ¨x, hxâŸ©) else g (e'.inv âŸ¨x, by simpa [hx] using x.propâŸ©),
        funext <| Î» x => by
        change (_ âˆ˜ _) x = f x
        dsimp
        split
        Â· change (e.hom â‰« e.inv â‰« f) x = f x
          rw [â†Category.assoc, e.hom_inv_id, Category.id_comp]
        Â· rename_i hx
          change (e.hom x : X) âˆ‰ A' at hx
          simp at hx,
        funext <| Î» x => by
        change (_ âˆ˜ _) x = g x
        dsimp
        split
        Â· rename_i hx
          simpa using hAB'.le (Set.mem_inter hx (e'.hom x).prop)
        Â· change (e'.hom â‰« e'.inv â‰« g) x = g x
          rw [â†Category.assoc, e'.hom_inv_id, Category.id_comp] âŸ©
      uniq := Î» h => by
        refine' Subtype.ext _
        funext x
        dsimp
        cases' x.prop with hx hx
        Â· have := h.2.1
          simp only [hx, dite_true]
          dsimp only at this
          simp_rw [â†this]
          refine' congr_arg _ (Subtype.ext _)
          dsimp
          change _ = Subtype.val ((e.inv â‰« e.hom) _)
          simp only [e.inv_hom_id]
          rfl
        Â· have hx' : (x : X) âˆ‰ A' := Î» H => by simpa using hAB'.le (Set.mem_inter H hx)
          have := h.2.2
          dsimp only at this
          simp only [â†this, hx', Category.assoc, dite_false]
          refine' congr_arg _ (Subtype.ext _)
          dsimp
          change _ = Subtype.val ((e'.inv â‰« e'.hom) _)
          simp only [e'.inv_hom_id]
          rfl }

noncomputable
def category57.disjoint_union_iso {X : Type _} (A B A' B' : Set X)
    (hAB : A âˆ© B = âˆ…) (hAB' : A' âˆ© B' = âˆ…) (e : A â‰… A') (e' : B â‰… B') :
    (âŸ¨A âˆª B, Î» x => âŸ¨x, by simpâŸ©, Î» x => âŸ¨x, by simpâŸ©âŸ© : (Î£ (Z : Set X), (A âŸ¶ Z) Ã— (B âŸ¶ Z))) â‰…
    (âŸ¨A' âˆª B', e.hom â‰« (Î» x => âŸ¨x, by simpâŸ©), e'.hom â‰« (Î» x => âŸ¨x, by simpâŸ©)âŸ©) :=
  Initial.Iso (category57.Initial_Union _ _ hAB) (category57.Initial_Union' _ _ _ _ hAB hAB' _ _)

-- 5.8. Show that in every category C the products A x B and B x A are isomorphic,
-- if they exist. (Hint: Observe that they both satisfy the universal property for
-- the product of A and B; then use Proposition 5.4.)

noncomputable
def AProd {C : Type _} [Category C] [hp : HasFiniteProducts C] (A B : C) : C :=
  (hp.hasFinal A B).choose.fst

local notation A:80 " Ã—_C " B:80 => AProd A B

noncomputable
def AProd.fstHom {C : Type _} [Category C] [hp : HasFiniteProducts C]
    {A B : C} : A Ã—_C B âŸ¶ A :=
  (hp.hasFinal A B).choose.snd.fst

noncomputable
def AProd.sndHom {C : Type _} [Category C] [hp : HasFiniteProducts C]
    {A B : C} : A Ã—_C B âŸ¶ B :=
  (hp.hasFinal A B).choose.snd.snd

lemma AProd_Final_sigma {C : Type _} [hC : Category C] [hp : HasFiniteProducts C] (A B : C) :
    Nonempty (Final (âŸ¨A Ã—_C B, AProd.fstHom, AProd.sndHomâŸ©
      : (Î£ (Z : C), hC.Hom Z A Ã— hC.Hom Z B))) :=
  (hp.hasFinal A B).choose_spec

lemma AProd_Final_sigma' {C : Type _} [hC : Category C] [hp : HasFiniteProducts C] (A B : C) :
    Nonempty (Final (âŸ¨B Ã—_C A, AProd.sndHom, AProd.fstHomâŸ©
      : (Î£ (Z : C), hC.Hom Z A Ã— hC.Hom Z B))) := by
  let âŸ¨FâŸ© := AProd_Final_sigma B A
  refine' âŸ¨Î» Z => _âŸ©
  obtain âŸ¨âŸ¨ÏƒâŸ©, hÏƒâŸ© := F âŸ¨Z.fst, Z.snd.snd, Z.snd.fstâŸ©
  refine' âŸ¨âŸ¨Ïƒ.val, Ïƒ.prop.symmâŸ©, Î» f => Subtype.ext _âŸ©
  specialize hÏƒ âŸ¨f.val, f.prop.symmâŸ©
  rw [Subtype.ext_iff.mp hÏƒ]
  rfl

lemma AProd.Iso {C : Type _} [hC : Category C] [hp : HasFiniteProducts C] (A B : C) :
    Nonempty (A Ã—_C B â‰… B Ã—_C A) := by
  obtain âŸ¨FâŸ© := (AProd_Final_sigma A B)
  obtain âŸ¨F'âŸ© := (AProd_Final_sigma' A B)
  let e := F.Iso F'
  exact âŸ¨e.hom.val, e.inv.val,
    congr_arg Subtype.val e.hom_inv_id, congr_arg Subtype.val e.inv_hom_idâŸ©

-- 5.9. Let C be a category with products. Find a reasonable candidate for the universal property
-- that the product A x B x C of three objects of C ought to satisfy, and prove that both
-- (A x B) x C and A x (B x C) satisfy this universal property.
-- Deduce that (A x B) x C and A x (B x C) are necessarily isomorphic

instance exercise59_down {C : Type _} [hC : Category C] (I J K : C) :
    Category (Î£ (Z : C), (Z âŸ¶ I) Ã— (Z âŸ¶ J) Ã— (Z âŸ¶ K)) where
  Hom := Î» âŸ¨Zâ‚, fâ‚âŸ© âŸ¨Zâ‚‚, fâ‚‚âŸ© => {Ïƒ : Zâ‚ âŸ¶ Zâ‚‚ // Ïƒ â‰« fâ‚‚.fst = fâ‚.fst âˆ§ Ïƒ â‰« fâ‚‚.snd.fst = fâ‚.snd.fst âˆ§ Ïƒ â‰« fâ‚‚.snd.snd = fâ‚.snd.snd}
  id  _ := âŸ¨ğŸ™ _, hC.id_comp _, hC.id_comp _, hC.id_comp _âŸ©
  comp := Î» âŸ¨f, hfâŸ© âŸ¨g, hgâŸ© => âŸ¨f â‰« g,
    by rw [hC.assoc, hg.left, hf.left],
    by rw [hC.assoc, hg.right.left, hf.right.left],
    by rw [hC.assoc, hg.right.right, hf.right.right]âŸ©
  id_comp := by
    intros
    exact Subtype.ext (hC.id_comp _)
  comp_id := by
    intros
    exact Subtype.ext (hC.comp_id _)
  assoc := by
    intros
    exact Subtype.ext (hC.assoc _ _ _)

class HasFiniteTripleProducts (C : Type _) [Category C] : Prop where
  hasFinal : âˆ€ I J K : C, âˆƒ F : (Î£ (Z : C), (Z âŸ¶ I) Ã— (Z âŸ¶ J) Ã— (Z âŸ¶ K)), Nonempty (Final F)

open AProd in
lemma TripleProduct_Final {C : Type _} [Category C] [hp : HasFiniteProducts C] (I J K : C) :
    Nonempty (Final (âŸ¨I Ã—_C J Ã—_C K, fstHom, sndHom â‰« fstHom, sndHom â‰« sndHomâŸ©
      : (Î£ (Z : C), (Z âŸ¶ I) Ã— (Z âŸ¶ J) Ã— (Z âŸ¶ K)))) := by
  let âŸ¨GâŸ© := AProd_Final_sigma J K
  let âŸ¨HâŸ© := AProd_Final_sigma I (J Ã—_C K)
  refine' âŸ¨_âŸ©
  intro âŸ¨Z, f, g, hâŸ©
  obtain âŸ¨âŸ¨Ï„âŸ©, hÏ„âŸ© := G âŸ¨Z, g, hâŸ©
  obtain âŸ¨âŸ¨Ï†âŸ©, hÏ†âŸ© := H âŸ¨Z, f, Ï„.valâŸ©
  refine' âŸ¨âŸ¨Ï†.val, Ï†.prop.left, _, _âŸ©, _âŸ©
  Â· dsimp
    rw [â†Category.assoc, Ï†.prop.right]
    exact Ï„.prop.left
  Â· dsimp
    rw [â†Category.assoc, Ï†.prop.right]
    exact Ï„.prop.right
  Â· intro Ïˆ
    refine' Subtype.ext _
    specialize hÏ„ âŸ¨Ïˆ.val â‰« sndHom, _, _âŸ©
    Â· simpa [Category.assoc] using Ïˆ.prop.right.left
    Â· simpa [Category.assoc] using Ïˆ.prop.right.right
    specialize hÏ† âŸ¨Ïˆ.val, Ïˆ.prop.left, _âŸ©
    Â· exact Subtype.ext_iff.mp hÏ„
    Â· dsimp
      exact Subtype.ext_iff.mp hÏ†

open AProd in
lemma TripleProduct_Final' {C : Type _} [Category C] [hp : HasFiniteProducts C] (I J K : C) :
    Nonempty (Final (âŸ¨(I Ã—_C J) Ã—_C K, fstHom â‰« fstHom, fstHom â‰« sndHom, sndHomâŸ©
      : (Î£ (Z : C), (Z âŸ¶ I) Ã— (Z âŸ¶ J) Ã— (Z âŸ¶ K)))) := by
  let âŸ¨GâŸ© := AProd_Final_sigma I J
  let âŸ¨HâŸ© := AProd_Final_sigma (I Ã—_C J) K
  refine' âŸ¨_âŸ©
  intro âŸ¨Z, f, g, hâŸ©
  obtain âŸ¨âŸ¨Ï„âŸ©, hÏ„âŸ© := G âŸ¨Z, f, gâŸ©
  obtain âŸ¨âŸ¨Ï†âŸ©, hÏ†âŸ© := H âŸ¨Z, Ï„.val, hâŸ©
  refine' âŸ¨âŸ¨Ï†.val, _, _, Ï†.prop.rightâŸ©, _âŸ©
  Â· dsimp
    rw [â†Category.assoc, Ï†.prop.left]
    exact Ï„.prop.left
  Â· dsimp
    rw [â†Category.assoc, Ï†.prop.left]
    exact Ï„.prop.right
  Â· intro Ïˆ
    refine' Subtype.ext _
    specialize hÏ„ âŸ¨Ïˆ.val â‰« fstHom, _, _âŸ©
    Â· simpa [Category.assoc] using Ïˆ.prop.left
    Â· simpa [Category.assoc] using Ïˆ.prop.right.left
    specialize hÏ† âŸ¨Ïˆ.val, _, Ïˆ.prop.right.rightâŸ©
    Â· exact Subtype.ext_iff.mp hÏ„
    Â· dsimp
      exact Subtype.ext_iff.mp hÏ†

lemma TripleProduct.Iso {C : Type _} [hC : Category C] [hp : HasFiniteProducts C] (I J K : C) :
    Nonempty (I Ã—_C J Ã—_C K â‰… (I Ã—_C J) Ã—_C K) := by
  obtain âŸ¨FâŸ© := (TripleProduct_Final I J K)
  obtain âŸ¨F'âŸ© := (TripleProduct_Final' I J K)
  let e := F.Iso F'
  exact âŸ¨e.hom.val, e.inv.val,
    congr_arg Subtype.val e.hom_inv_id, congr_arg Subtype.val e.inv_hom_idâŸ©

-- 5.10. Push the envelope a little further still, and define products and coproducts for families
-- (i.e., indexed sets) of objects of a category.  Do these exist in Set?

instance exercise510_down (C : Type u) [Category.{v} C] (I : Type w) (Î¹ : I â†’ C) :
    Category (Î£ (Z : C), âˆ€ i : I, Z âŸ¶ Î¹ i) where
  Hom := Î» âŸ¨X, fâŸ© âŸ¨Y, gâŸ© => {Ïƒ : X âŸ¶ Y // âˆ€ i, Ïƒ â‰« g i = f i}
  id _ := âŸ¨ğŸ™ _, Î» _ => Category.id_comp _âŸ©
  comp := Î» âŸ¨f, hfâŸ© âŸ¨g, hgâŸ© => âŸ¨f â‰« g, Î» _ => by rw [Category.assoc, hg, hf]âŸ©
  id_comp := by
    intros
    exact Subtype.ext (Category.id_comp _)
  comp_id := by
    intros
    exact Subtype.ext (Category.comp_id _)
  assoc := by
    intros
    exact Subtype.ext (Category.assoc _ _ _)

instance exercise510_up (C : Type u) [Category.{v} C] (I : Type w) (Î¹ : I â†’ C) :
    Category (Î£ (Z : C), âˆ€ i : I, Î¹ i âŸ¶ Z) where
  Hom := Î» âŸ¨X, fâŸ© âŸ¨Y, gâŸ© => {Ïƒ : X âŸ¶ Y // âˆ€ i, f i â‰« Ïƒ = g i}
  id _ := âŸ¨ğŸ™ _, Î» _ => Category.comp_id _âŸ©
  comp := Î» âŸ¨f, hfâŸ© âŸ¨g, hgâŸ© => âŸ¨f â‰« g, Î» _ => by rw [â†Category.assoc, hf, hg]âŸ©
  id_comp := by
    intros
    exact Subtype.ext (Category.id_comp _)
  comp_id := by
    intros
    exact Subtype.ext (Category.comp_id _)
  assoc := by
    intros
    exact Subtype.ext (Category.assoc _ _ _)

class HasIndexedProducts (C : Type u) [Category.{v} C] (I : Type w) : Prop where
  hasFinal : âˆ€ Î¹ : I â†’ C, âˆƒ F : (Î£ (Z : C), âˆ€ i : I, Z âŸ¶ Î¹ i), Nonempty (Final F)

class HasIndexedCoproducts (C : Type u) [Category.{v} C] (I : Type w) : Prop where
  hasFinal : âˆ€ Î¹ : I â†’ C, âˆƒ F : (Î£ (Z : C), âˆ€ i : I, Î¹ i âŸ¶ Z), Nonempty (Initial F)


-- Set (Type 0) has indexed products, which are Pi types
instance (I : Type u) : HasIndexedProducts (Type u) I where
  hasFinal Î¹ := âŸ¨âŸ¨âˆ€ i : I, Î¹ i, Î» i f => f iâŸ©, âŸ¨Î» âŸ¨Z, fâŸ© => âŸ¨âŸ¨Î» z i => f i z, Î» _ => rflâŸ©, Î» Î¹ => by
    refine' Subtype.ext _
    funext i z
    exact congr_fun (Î¹.prop z) iâŸ©âŸ©âŸ©

-- Set (Type 0) has indexed coproducts, which are Sigma types
instance (I : Type u) : HasIndexedCoproducts (Type u) I where
  hasFinal Î¹ := âŸ¨âŸ¨Î£ i : I, Î¹ i, Sigma.mkâŸ©, âŸ¨Î» âŸ¨Z, fâŸ© => âŸ¨âŸ¨Î» z => f z.fst z.snd, Î» _ => rflâŸ©,
    Î» Î¹ => by
      refine' Subtype.ext _
      funext i
      exact congr_fun (Î¹.prop i.fst) i.sndâŸ©âŸ©âŸ©

-- 5.11. Let A, resp. B, be a set, endowed with an equivalence relation ~A, resp. ~B.
-- Define a relation ~ on A x B by setting
-- (aâ‚, bâ‚) ~ (aâ‚‚, bâ‚‚) â†” aâ‚ ~A aâ‚‚ and bâ‚ ~B bâ‚‚.
-- (This is immediately seen to be an equivalence relation.)
-- Use the universal property for quotients (Â§5.3) to establish that there are functions
-- (A x B)/~ -> A/-A, (A x B)/~ -> B/-B. Prove that (A x B)/~, with these two functions,
-- satisfies the universal property for the product of A/~A and B/~B.
-- Conclude (without further work) that (A x B)/~ â‰… (A/~A) X (B/~B).

section exercise511

variable {A B : Type u} (r : Setoid A) (s : Setoid B)

def Setoid.prod : Setoid (A Ã— B) where
  r x y := r.r x.fst y.fst âˆ§ s.r x.snd y.snd
  iseqv := âŸ¨Î» _ => And.intro (r.refl _) (s.refl _), And.imp r.symm s.symm,
    Î» âŸ¨hr, hsâŸ© âŸ¨hr', hs'âŸ© => And.intro (r.trans hr hr') (s.trans hs hs')âŸ©

def concreteProd :
    Final (âŸ¨A Ã— B, Prod.fst, Prod.sndâŸ© : (Î£ (Z : Type u), (Z âŸ¶ A) Ã— (Z âŸ¶ B))) :=
  Î» âŸ¨_, f, gâŸ© => âŸ¨âŸ¨Î» z => âŸ¨f z, g zâŸ©, rfl, rflâŸ©,
    Î» âŸ¨_, hf, hf'âŸ© => Subtype.ext (funext Î» _ => Prod.ext (congr_fun hf _) (congr_fun hf' _))âŸ©

instance concreteHasProd : HasFiniteProducts (Type u) where
  hasFinal A B := âŸ¨_, âŸ¨concreteProd (A := A) (B := B)âŸ©âŸ©

noncomputable
def HomAProd {C : Type u} [Category C] [hp : HasFiniteProducts C] {Z A B : C}
    (f : Z âŸ¶ A) (g : Z âŸ¶ B) : Z âŸ¶ A Ã—_C B :=
  (Classical.choice (hp.hasFinal A B).choose_spec âŸ¨Z, f, gâŸ©).default.val

@[simp]
lemma HomAProd_comp_fst {C : Type u} [Category C] [hp : HasFiniteProducts C] {Z A B : C}
      (f : Z âŸ¶ A) (g : Z âŸ¶ B) : HomAProd f g â‰« AProd.fstHom = f :=
  (Classical.choice (hp.hasFinal A B).choose_spec âŸ¨Z, f, gâŸ©).default.prop.left

@[simp]
lemma HomAProd_comp_snd {C : Type u} [Category C] [hp : HasFiniteProducts C] {Z A B : C}
      (f : Z âŸ¶ A) (g : Z âŸ¶ B) : HomAProd f g â‰« AProd.sndHom = g :=
  (Classical.choice (hp.hasFinal A B).choose_spec âŸ¨Z, f, gâŸ©).default.prop.right

def QuotHom {Z : Type _} (r : Z â†’ Z â†’ Prop) : Z âŸ¶ Quot r := Quot.mk _

@[simp]
lemma QuotHom_apply {Z : Type _} (r : Z â†’ Z â†’ Prop) (a : Z) : QuotHom r a = Quot.mk r a := rfl

def QuotLift {A Z : Type _} (f : A âŸ¶ Z) (r : A â†’ A â†’ Prop) (hf : âˆ€ a b, r a b â†’ f a = f b) :
    Quot r âŸ¶ Z := ((Quot_Initial r) âŸ¨Z, f, hfâŸ©).default.val

lemma QuotLift_prop {A Z : Type _} (f : A âŸ¶ Z) (r : A â†’ A â†’ Prop) (hf : âˆ€ a b, r a b â†’ f a = f b) :
  QuotHom r â‰« QuotLift f r hf = f := ((Quot_Initial r) âŸ¨Z, f, hfâŸ©).default.prop

lemma QuotLift_unique {A Z : Type _} (f : A âŸ¶ Z) (r : A â†’ A â†’ Prop) (hf : âˆ€ a b, r a b â†’ f a = f b)
    (g : Quot r âŸ¶ Z) (hg : QuotHom r â‰« g = f) : g = QuotLift f r hf := by
  rw [â†QuotLift_prop f r hf] at hg
  have := ((Quot_Initial r) âŸ¨Z, f, hfâŸ©).uniq âŸ¨g, hgâŸ©
  exact Subtype.ext_iff.mp this

@[simp]
lemma QuotLift_apply {A Z : Type _} (f : A âŸ¶ Z) (r : A â†’ A â†’ Prop) (hf : âˆ€ a b, r a b â†’ f a = f b)
    (a : A) : QuotLift f r hf (Quot.mk r a) = f a := rfl

def QuotMap {A Z : Type u} (r : A â†’ A â†’ Prop) (s : Z â†’ Z â†’ Prop) (f : A âŸ¶ Z)
    (hf : âˆ€ a b, r a b â†’ s (f a) (f b)) : Quot r âŸ¶ Quot s :=
  Quot.lift (f â‰« QuotHom _) (Î» a b h => Quot.sound (hf a b h))

@[simp] lemma QuotMap_apply {A Z : Type u} (r : A â†’ A â†’ Prop) (s : Z â†’ Z â†’ Prop) (f : A âŸ¶ Z)
    (hf : âˆ€ a b, r a b â†’ s (f a) (f b)) (a : A) :
    QuotMap r s f hf (Quot.mk r a) = Quot.mk s (f a) :=
  rfl

def QuotFst : Quot (r.prod s).r âŸ¶ Quot r.r := QuotMap _ _ (Prod.fst (Î± := A)) $ by
  rintro âŸ¨a, bâŸ© âŸ¨x, yâŸ© âŸ¨h, -âŸ©
  exact h

def QuotSnd : Quot (r.prod s).r âŸ¶ Quot s.r := QuotMap _ _ (Prod.snd (Î² := B)) $ by
  rintro âŸ¨a, bâŸ© âŸ¨x, yâŸ© âŸ¨-, hâŸ©
  exact h

lemma ProdQuot_Final :
    Final (âŸ¨Quot (r.prod s).r, QuotFst _ _, QuotSnd _ _âŸ© :
      (Î£ (Z : Type u), (Z âŸ¶ Quot r.r) Ã— (Z âŸ¶ Quot s.r))) :=
  Î» âŸ¨Z, f, gâŸ© => âŸ¨âŸ¨
    Î» z => Quot.liftâ‚‚ (Î» a b => Quot.mk _ âŸ¨a, bâŸ©)
    (Î» _ _ _' h => Quot.sound âŸ¨r.refl _, hâŸ©) (Î» _ _ _ h => Quot.sound âŸ¨h, s.refl _âŸ©) (f z) (g z),
    by
      funext a
      simp only [QuotFst, types_comp_apply]
      induction' f a using Quot.induction_on
      induction' g a using Quot.induction_on
      simp
    , by
      funext a
      simp only [QuotSnd, types_comp_apply]
      induction' f a using Quot.induction_on
      induction' g a using Quot.induction_on
      simpâŸ©,
    Î» âŸ¨w, hw, hw'âŸ© => by
      refine' Subtype.ext _
      dsimp only at hw hw'
      funext z
      simp only [â†hw, â†hw', types_comp_apply]
      induction' w z using Quot.induction_on with z
      rcases z with âŸ¨a, bâŸ©
      simp [QuotFst, QuotSnd]âŸ©

end exercise511

-- 5.12. Define the notions of fibered products and fibered coproducts, as terminal objects of the
-- categories C_Î±,Î², C^Î±,Î² considered in Example 3.10 (cf. also Exercise 3.11),
-- by stating carefully the corresponding universal properties.
-- As it happens, Set has both fibered products and coproducts.
-- Define these objects `concretely', in terms of naive yet theory. [III.6.10, III.6.11]

instance example310_down {D : Type _} [hD : Category D] {A B C : D} (Î± : A âŸ¶ C) (Î² : B âŸ¶ C) :
    Category (Î£ (Z : D), {fg : hD.Hom Z A Ã— hD.Hom Z B // fg.fst â‰« Î± = fg.snd â‰« Î²}) where
  Hom := Î» âŸ¨Zâ‚, âŸ¨fâ‚, gâ‚âŸ©, _âŸ© âŸ¨Zâ‚‚, âŸ¨fâ‚‚, gâ‚‚âŸ©, _âŸ© => {Ïƒ : Zâ‚ âŸ¶ Zâ‚‚ // Ïƒ â‰« fâ‚‚ = fâ‚ âˆ§ Ïƒ â‰« gâ‚‚ = gâ‚}
  id _ := âŸ¨ğŸ™ _, hD.id_comp _, hD.id_comp _âŸ©
  comp := Î» âŸ¨f, hfâŸ© âŸ¨g, hgâŸ© => âŸ¨f â‰« g,
    by rw [hD.assoc, hg.left, â†hf.left],
    by rw [hD.assoc, hg.right, â†hf.right]âŸ©
  id_comp := by
    intros
    exact Subtype.ext (hD.id_comp _)
  comp_id := by
    intros
    exact Subtype.ext (hD.comp_id _)
  assoc := by
    intros
    exact Subtype.ext (hD.assoc _ _ _)

instance example310_up {D : Type _} [hD : Category D] {A B C : D} (Î± : C âŸ¶ A) (Î² : C âŸ¶ B) :
    Category (Î£ (Z : D), {fg : hD.Hom A Z Ã— hD.Hom B Z // Î± â‰« fg.fst = Î² â‰« fg.snd}) where
  Hom := Î» âŸ¨Zâ‚, âŸ¨fâ‚, gâ‚âŸ©, _âŸ© âŸ¨Zâ‚‚, âŸ¨fâ‚‚, gâ‚‚âŸ©, _âŸ© => {Ïƒ : Zâ‚ âŸ¶ Zâ‚‚ // fâ‚ â‰« Ïƒ = fâ‚‚ âˆ§ gâ‚ â‰« Ïƒ = gâ‚‚}
  id _ := âŸ¨ğŸ™ _, hD.comp_id _, hD.comp_id _âŸ©
  comp := Î» âŸ¨f, hfâŸ© âŸ¨g, hgâŸ© => âŸ¨f â‰« g,
    by rw [â†hD.assoc, â†hg.left, â†hf.left],
    by rw [â†hD.assoc, â†hg.right, â†hf.right]âŸ©
  id_comp := by
    intros
    exact Subtype.ext (hD.id_comp _)
  comp_id := by
    intros
    exact Subtype.ext (hD.comp_id _)
  assoc := by
    intros
    exact Subtype.ext (hD.assoc _ _ _)

class HasFiberedProducts {D : Type _} [hD : Category D] {A B C : D} (Î± : A âŸ¶ C) (Î² : B âŸ¶ C) where
  HasFinal : âˆƒ (F : (Î£ (Z : D), {fg : hD.Hom Z A Ã— hD.Hom Z B // fg.fst â‰« Î± = fg.snd â‰« Î²})), Nonempty (Final F)

class HasFiberedCoproducts {D : Type _} [hD : Category D] {A B C : D} (Î± : C âŸ¶ A) (Î² : C âŸ¶ B) where
  hasInitial : âˆƒ (I : (Î£ (Z : D), {fg : hD.Hom A Z Ã— hD.Hom B Z // Î± â‰« fg.fst = Î² â‰« fg.snd})), Nonempty (Initial I)

def FProd_Final {A B C : Type u} (f : A âŸ¶ C) (g : B âŸ¶ C) :
    Final (C := (Î£ (Z : Type u), {fg : (Z âŸ¶ A) Ã— (Z âŸ¶ B) // fg.fst â‰« f = fg.snd â‰« g}))
    âŸ¨{xy : A Ã— B // f xy.fst = g xy.snd}, âŸ¨Subtype.val â‰« Prod.fst, Subtype.val â‰« Prod.sndâŸ©,
    funext $ Î» x => x.propâŸ© :=
  Î» âŸ¨_, âŸ¨f', g'âŸ©, hâŸ© => âŸ¨âŸ¨Î» z => âŸ¨âŸ¨f' z, g' zâŸ©, congr_fun h zâŸ©, funext Î» _ => rfl,
    funext Î» _ => rflâŸ©, Î» âŸ¨_, hu, hu'âŸ© =>
    Subtype.ext (funext Î» _ => Subtype.ext (Prod.ext (congr_fun hu _) (congr_fun hu' _)))âŸ©

instance exercise512_down {A B C : Type u} (f : A âŸ¶ C) (g : B âŸ¶ C) :
    HasFiberedProducts f g where
  HasFinal := âŸ¨_, âŸ¨FProd_Final f gâŸ©âŸ©

noncomputable -- needs Sum.rec to be computable
def FCoprod_Initial {A B C : Type u} (f : C âŸ¶ A) (g : C âŸ¶ B) :
    Initial (C := (Î£ (Z : Type u), {fg : (A âŸ¶ Z) Ã— (B âŸ¶ Z) // f â‰« fg.fst = g â‰« fg.snd}))
    âŸ¨Quot (Î» (a b : A âŠ• B) => a = b âˆ¨ âˆƒ c : C, (a= Sum.inl (f c) âˆ§ b = Sum.inr (g c)) âˆ¨
        (a = Sum.inr (g c) âˆ§ b = Sum.inl (f c))),
    âŸ¨Sum.inl â‰« Quot.mk _, Sum.inr â‰« Quot.mk _âŸ©,
     funext Î» z => Quot.sound (Or.inr âŸ¨z, Or.inl âŸ¨rfl, rflâŸ©âŸ©)âŸ© :=
  Î» âŸ¨Z, âŸ¨u, vâŸ©, hâŸ© => âŸ¨âŸ¨Quot.lift (Sum.rec u v) $ by
      rintro ab ab' (rfl|âŸ¨c, (âŸ¨rfl, rflâŸ©|âŸ¨rfl, rflâŸ©)âŸ©)
      Â· rfl
      Â· exact congr_fun h _
      Â· exact congr_fun h.symm _,
    funext Î» _ => by rfl,
    funext Î» _ => by rflâŸ©,
  Î» w => Subtype.ext (funext Î» c => Quot.inductionOn c $ by
    rintro (a|b)
    Â· exact congr_fun w.prop.left a
    Â· exact congr_fun w.prop.right b)âŸ©

instance exercise512_up {A B C : Type u} (f : C âŸ¶ A) (g : C âŸ¶ B) :
    HasFiberedCoproducts f g where
  hasInitial := âŸ¨_, âŸ¨FCoprod_Initial f gâŸ©âŸ©
