import Mathlib.CategoryTheory.Groupoid
import Mathlib.Algebra.Category.Ring.Basic
import Mathlib.Data.Setoid.Basic

set_option autoImplicit false

universe u v

open CategoryTheory

-- 4.2. In Example 3.3 we have seen how to construct a category from a set endowed with a relation,
-- provided this latter is reflexive and transitive. For what types of relations is the
-- corresponding category a groupoid (cf. Example 4.6)? [Â§4.11]

inductive example33Hom {S : Type u} (r : S â†’ S â†’ Prop) (a b : S)
| ofRel : r a b â†’ example33Hom r a b

def example33Hom.rel {S : Type u} {r : S â†’ S â†’ Prop} {a b : S} (R : example33Hom r a b) :
    r a b := by
  cases R with
  | ofRel h => exact h

set_option synthInstance.checkSynthOrder false in
instance example33 (S : Type u) (r : S â†’ S â†’ Prop) [IsRefl S r] [IsTrans S r] : Category S where
  Hom a b := example33Hom r a b
  id a := .ofRel (refl a)
  comp := Î» âŸ¨fâŸ© âŸ¨gâŸ© => .ofRel (_root_.trans f g)
  id_comp := by
    rintro a b âŸ¨fâŸ©
    -- there is no `.ofRel.inj`??
    exact congrArg example33Hom.ofRel (rfl : _root_.trans (refl a) f = f)
  comp_id := by
    rintro a b âŸ¨fâŸ©
    exact congrArg example33Hom.ofRel (rfl : f = _root_.trans f (refl b))
  assoc := by
    rintro a b c d âŸ¨fâŸ© âŸ¨gâŸ© âŸ¨hâŸ©
    have : _root_.trans (_root_.trans f g) h = _root_.trans f (_root_.trans g h) := rfl
    dsimp
    exact congrArg example33Hom.ofRel this

def exercise42 (S : Type u) (r : S â†’ S â†’ Prop) [IsRefl S r] [IsSymm S r] [IsTrans S r] :
    Groupoid S where
  -- How do you inherit from `example33 S r`?
  Hom a b := example33Hom r a b
  id a := .ofRel (refl a)
  comp := Î» âŸ¨fâŸ© âŸ¨gâŸ© => .ofRel (_root_.trans f g)
  id_comp := by
    rintro a b âŸ¨fâŸ©
    exact congrArg example33Hom.ofRel (rfl : _root_.trans (refl a) f = f)
  comp_id := by
    rintro a b âŸ¨fâŸ©
    exact congrArg example33Hom.ofRel (rfl : f = _root_.trans f (refl b))
  assoc := by
    rintro a b c d âŸ¨fâŸ© âŸ¨gâŸ© âŸ¨hâŸ©
    have : _root_.trans (_root_.trans f g) h = _root_.trans f (_root_.trans g h) := rfl
    dsimp
    exact congrArg example33Hom.ofRel this
  inv := Î» f => .ofRel (symm f.rel) -- using destruction here causes metavariables, #2071
  inv_comp := by
    rintro a b âŸ¨fâŸ©
    exact congrArg example33Hom.ofRel (rfl : _root_.trans (symm f) f = refl b)
  comp_inv := by
    rintro a b âŸ¨fâŸ©
    exact congrArg example33Hom.ofRel (rfl : _root_.trans f (symm f) = refl a)

-- 4.3. Let A, B be objects of a category C, and let f âˆˆ Hom_C(A, B) be a morphism.
-- - Prove that if f has a right-inverse, then f is an epimorphism.
-- - Show that the converse does not hold, by giving an explicit example of a category and an
-- epimorphism without a right-inverse.
example {C : Type _} [hC : Category C] (A B : C) (f : A âŸ¶ B) (hf : âˆƒ g, g â‰« f = ğŸ™ _) :
    Epi f where
  left_cancellation g h H := by
    obtain âŸ¨u, huâŸ© := hf
    rw [â†hC.id_comp g, â†hu, hC.assoc, H, â†hC.assoc, hu, hC.id_comp]

-- Porting note: I think this is now redundant.
@[simp]
theorem RingCat.ofHom_apply {R S : Type u} [Ring R] [Ring S] (f : R â†’+* S) (x : R) :
    RingCat.ofHom f x = f x := rfl
set_option linter.uppercaseLean3 false in
#align Ring.of_hom_apply RingCat.ofHom_apply

theorem exercise43epi : Epi (RingCat.ofHom (Int.castRingHom â„š)) where
  left_cancellation := by
    intros X g h H
    have hc : âˆ€ z : â„¤, g z = h z
    Â· intro z
      have : (z : â„š) = Int.castRingHom â„š z := rfl
      rw [this, â†RingCat.ofHom_apply, â†Function.comp_apply (f := g),
          â†RingCat.coe_comp (g := g) (f := RingCat.ofHom (Int.castRingHom â„š)), H,
          RingCat.coe_comp, Function.comp_apply, RingCat.ofHom_apply]
    have hc' : âˆ€ z : â„•, g z = h z
    Â· intro z
      exact_mod_cast hc z
    ext a
    rcases a with âŸ¨num, den, hden, hcoprimeâŸ©
    dsimp only [RingCat.forget_map, RingCat.coe_of]
    have : Rat.mk' num den hden hcoprime = num * (â†‘den)â»Â¹
    Â· rw [â†div_eq_mul_inv, Rat.div_num_den]
      simp [Rat.normalize_eq_mkRat, â†Rat.normalize_eq_mk']
    have hd : (den : â„š) â‰  0 := by exact_mod_cast hden
    rw [this, map_mul, map_mul, hc, â†div_mul_cancel (num : â„š) hd, map_mul, â†hc', mul_assoc,
        â†map_mul, hc', mul_assoc, â†map_mul, mul_inv_cancel hd, map_one, mul_one, map_one, mul_one]

@[simp]
theorem Rat.ratCast_eq (x : â„š) : Rat.cast x = x := rfl

theorem exercise43not_inv : âˆ€ g, Â¬ g â‰« (RingCat.ofHom (Int.castRingHom â„š)) = ğŸ™ _ := by
  intro g H
  have hg : âˆ€ x : â„š, Int.castRingHom â„š (g x) = x
  Â· intro x
    rw [â†RingCat.ofHom_apply (Int.castRingHom â„š),
        â†Function.comp_apply (f := RingCat.ofHom _) (g := g),
        â†RingCat.coe_comp (f := g) (g := RingCat.ofHom (Int.castRingHom â„š)), H]
    simp
  have hinj : âˆ€ x y, Int.castRingHom â„š x = Int.castRingHom â„š y â†’ x = y
  Â· intros x y h
    exact Int.cast_injective h
  have hg : âˆ€ n : â„•, 0 < n â†’ n * g (1 / n : â„š) = g 1
  Â· intro n hn
    simp only [RingCat.coe_of, one_div]
    apply hinj
    rw [map_mul, hg, hg]
    simp only [map_natCast, ne_eq, Nat.cast_eq_zero]
    rw [mul_inv_cancel]
    exact_mod_cast hn.ne'
  replace hg : âˆ€ n : â„•, 0 < n â†’ (n : â„¤) âˆ£ 1
  Â· intros n hn
    specialize hg n hn
    rw [map_one] at hg
    rw [â†hg]
    simp
  specialize hg 2 zero_lt_two
  simpa using Int.eq_one_of_dvd_one zero_le_two hg

-- 4.4. Prove that the composition of two monomorphisms is a monomorphism.
-- Deduce that one can define a subcategory C_mono of a category C by taking the same objects as in
-- C and defining Hom_C_mono(A, B) to be the subset of Hom_C(A, B) consisting of monomorphisms,
-- for all objects A, B. (Cf. Exercise 3.8; of course, in general C_mono is not full in C.)
-- Do the same for epimorphisms. Can you define a subcategory C_nonmono of C by restricting to
-- morphisms that are not monomorphisms?
theorem exercise44mono {C : Type _} [Category C] {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)
    (hf : Mono f) (hg : Mono g) : Mono (f â‰« g) where
  right_cancellation := by
    intros W u v H
    rw [â†Category.assoc, â†Category.assoc] at H
    replace hg := hg.right_cancellation _ _ H
    exact hf.right_cancellation _ _ hg

def exercise44monocat {C : Type _} [hC : Category C] : Category C where
  Hom a b := {f : a âŸ¶ b // Mono f}
  id a := âŸ¨ğŸ™ a, âŸ¨by
    intros
    simp_all [Category.comp_id]âŸ©âŸ©
  comp f g := âŸ¨f.val â‰« g.val, exercise44mono f.val g.val f.prop g.propâŸ©
  id_comp f := Subtype.ext (Category.id_comp _)
  comp_id f := Subtype.ext (Category.comp_id _)
  assoc _ _ _ := Subtype.ext (Category.assoc _ _ _)

theorem exercise44epi {C : Type _} [Category C] {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)
    (hf : Epi f) (hg : Epi g) : Epi (f â‰« g) where
  left_cancellation := by
    intros W u v H
    rw [Category.assoc, Category.assoc] at H
    replace hf := hf.left_cancellation _ _ H
    exact hg.left_cancellation _ _ hf

def exercise44epicat {C : Type _} [hC : Category C] : Category C where
  Hom a b := {f : a âŸ¶ b // Epi f}
  id a := âŸ¨ğŸ™ a, âŸ¨by
    intros
    simp_all [Category.comp_id]âŸ©âŸ©
  comp f g := âŸ¨f.val â‰« g.val, exercise44epi f.val g.val f.prop g.propâŸ©
  id_comp f := Subtype.ext (Category.id_comp _)
  comp_id f := Subtype.ext (Category.comp_id _)
  assoc _ _ _ := Subtype.ext (Category.assoc _ _ _)

-- Can't define a category C_nonmono of not monomorphisms because then we wouldn't have
-- identity morphisms, which are necessary monomorphisms

-- 4.5. Give a concrete description of monomorphisms and epimorphisms in the category MSet you
-- constructed in Exercise 3.9.
-- (Your answer will depend on the notion of morphism you defined in that exercise!)

instance exercise39 : Category (Î£ X : Type u, Setoid X) where
  Hom := Î» âŸ¨X, X'âŸ© âŸ¨Y, Y'âŸ© => {f : X âŸ¶ Y // âˆ€ â¦ƒa b : Xâ¦„, a â‰ˆ b â†’ (f a) â‰ˆ (f b)}
  id := Î» âŸ¨X, X'âŸ© => âŸ¨id, Î» _ _ h => hâŸ©
  comp := Î» âŸ¨f, hfâŸ© âŸ¨g, hgâŸ© => âŸ¨g âˆ˜ f, Î» _ _ h => hg (hf h)âŸ©
  id_comp := by
    intros
    exact Subtype.ext (Function.comp.right_id _)
  comp_id := by
    intros
    exact Subtype.ext (Function.comp.left_id _)
  assoc := by
    intros
    exact Subtype.ext (Function.comp.assoc _ _ _)

theorem exercise45mono {A B : (Î£ X : Type u, Setoid X)} (f : A âŸ¶ B) :
    Mono f â†” Mono f.val := by
  constructor <;> intro h <;> constructor
  Â· intros Z u v H
    let Z' : (Î£ X : Type u, Setoid X) := âŸ¨Z, âŠ¥âŸ©
    let u' : Z' âŸ¶ A := âŸ¨u, Î» _ _ H => by rw [H]; exact A.2.2.1 _âŸ©
    let v' : Z' âŸ¶ A := âŸ¨v, Î» _ _ H => by rw [H]; exact A.2.2.1 _âŸ©
    change u'.val = v'.val
    rw [â†Subtype.ext_iff]
    refine' h.right_cancellation _ _ _
    exact Subtype.ext H
  Â· intros Z u v H
    rw [Subtype.ext_iff] at H âŠ¢
    exact h.right_cancellation _ _ H

theorem exercise45epi {A B : (Î£ X : Type u, Setoid X)} (f : A âŸ¶ B) :
    Epi f â†” Epi f.val := by
  constructor <;> intro h <;> constructor
  Â· intros Z u v H
    let Z' : (Î£ X : Type u, Setoid X) := âŸ¨Z, âŠ¤âŸ©
    let u' : B âŸ¶ Z' := âŸ¨u, Î» _ _ _ => by simp; trivialâŸ©
    let v' : B âŸ¶ Z' := âŸ¨v, Î» a _ _ => by simp; trivialâŸ©
    change u'.val = v'.val
    rw [â†Subtype.ext_iff]
    refine' h.left_cancellation _ _ _
    exact Subtype.ext H
  Â· intros Z u v H
    rw [Subtype.ext_iff] at H âŠ¢
    exact h.left_cancellation _ _ H
