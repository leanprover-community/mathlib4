module

public import Mathlib.AlgebraicGeometry.Morphisms.Flat
public import Mathlib.RingTheory.TensorProduct.Pi
public import Mathlib.Topology.Sheaves.SheafCondition.EqualizerProducts
public import Mathlib.Algebra.Category.CommAlgCat.Basic
public import Mathlib.Algebra.Category.ModuleCat.ChangeOfRings
public import Mathlib.Algebra.Category.AlgCat.Limits
public import Mathlib.Algebra.Category.ModuleCat.AB
public import Mathlib.Tactic.DeriveFintype

@[expose] public section

open CategoryTheory CategoryTheory.Limits Opposite TopologicalSpace

universe v u

variable {R S : CommRingCat.{u}} (œÜ : R ‚ü∂ S) (hœÜ : œÜ.hom.Flat)

def CommAlgCat.fullyFaithful_forget‚ÇÇ_algCat :
    (forget‚ÇÇ (CommAlgCat R) (AlgCat R)).FullyFaithful where
  preimage f := CommAlgCat.ofHom f.hom

instance : (forget‚ÇÇ (CommAlgCat R) (AlgCat R)).Full := CommAlgCat.fullyFaithful_forget‚ÇÇ_algCat.full
instance : (forget‚ÇÇ (CommAlgCat R) (AlgCat R)).Faithful :=
  CommAlgCat.fullyFaithful_forget‚ÇÇ_algCat.faithful

instance {R : Type u} [CommRing R] :
    PreservesLimits (forget‚ÇÇ (AlgCat.{u} R) (ModuleCat R)) :=
  have : PreservesLimits (forget‚ÇÇ (AlgCat R) (ModuleCat R) ‚ãô forget (ModuleCat R)) := by
    change PreservesLimits (forget _)
    infer_instance
  preservesLimits_of_reflects_of_preserves
    (F := forget‚ÇÇ (AlgCat R) (ModuleCat R)) (G := forget _)

instance {R : Type u} [CommRing R] :
    PreservesLimits (forget (CommAlgCat.{u} R)) :=
  inferInstanceAs (PreservesLimits <| (commAlgCatEquivUnder (.of R)).functor ‚ãô
    Under.forget _ ‚ãô forget _)

instance {R : Type u} [CommRing R] : ReflectsLimits (forget (AlgCat.{u} R)) :=
  reflectsLimits_of_reflectsIsomorphisms

instance {R : Type u} [CommRing R] :
    PreservesLimits (forget‚ÇÇ (CommAlgCat.{u} R) (AlgCat.{u} R)) :=
  have : PreservesLimits (forget‚ÇÇ (CommAlgCat R) (AlgCat R) ‚ãô forget (AlgCat R)) := by
    change PreservesLimits (forget _)
    infer_instance
  preservesLimits_of_reflects_of_preserves
    (F := forget‚ÇÇ (CommAlgCat R) (AlgCat R)) (G := forget _)

instance {R : Type u} [CommRing R] :
    (forget‚ÇÇ (AlgCat.{u} R) (ModuleCat R)).ReflectsIsomorphisms :=
  have : (forget‚ÇÇ (AlgCat R) (ModuleCat R) ‚ãô forget (ModuleCat R)).ReflectsIsomorphisms := by
    change (forget _).ReflectsIsomorphisms
    infer_instance
  reflectsIsomorphisms_of_comp _ (forget _)

instance {R : Type u} [CommRing R] :
    ReflectsLimits (forget‚ÇÇ (AlgCat.{u} R) (ModuleCat R)) :=
  reflectsLimits_of_reflectsIsomorphisms

instance {R S : Type*} [CommRing R] [CommRing S] (f : R ‚Üí+* S) :
    (ModuleCat.extendScalars f).Additive where
  map_add {M N œÜ œà} := by ext; simp [TensorProduct.tmul_add]

lemma ModuleCat.preservesFiniteLimits_extendScalars.{u‚ÇÅ, u‚ÇÇ, w}
    {R : Type u‚ÇÅ} {S : Type u‚ÇÇ} [CommRing R] [CommRing S] (f : R ‚Üí+* S) (hf : f.Flat) :
    PreservesFiniteLimits (ModuleCat.extendScalars.{u‚ÇÅ, u‚ÇÇ, max w u‚ÇÇ} f) := by
  have := ((ModuleCat.extendScalars.{u‚ÇÅ, u‚ÇÇ, max w u‚ÇÇ}
    f).preservesFiniteColimits_tfae.out 3 0 rfl).mp inferInstance
  rw [(ModuleCat.extendScalars.{u‚ÇÅ, u‚ÇÇ, max w u‚ÇÇ} f).preservesFiniteLimits_tfae.out 3 0 rfl]
  rintro S hS
  refine ‚ü®(this S hS).1, ?_‚ü©
  rw [ModuleCat.mono_iff_injective]
  algebraize [f]
  exact Module.Flat.lTensor_preserves_injective_linearMap _
    ((ModuleCat.mono_iff_injective _).mp hS.mono_f)

open TensorProduct in
noncomputable
def pushoutIsoExtendScalars : (((Under.pushout œÜ ‚ãô (commAlgCatEquivUnder S).inverse) ‚ãô
    forget‚ÇÇ (CommAlgCat ‚ÜëS) (AlgCat ‚ÜëS)) ‚ãô forget‚ÇÇ (AlgCat ‚ÜëS) (ModuleCat ‚ÜëS)) ‚âÖ
      (commAlgCatEquivUnder R).inverse ‚ãô forget‚ÇÇ (CommAlgCat R) (AlgCat R) ‚ãô
        forget‚ÇÇ (AlgCat R) (ModuleCat R) ‚ãô ModuleCat.extendScalars œÜ.hom := by
  letI := œÜ.hom.toAlgebra
  letI e (A : Under R) : pushout A.hom œÜ ‚âÖ .of (S ‚äó[R] A.right) :=
    pushoutSymmetry _ _ ‚â™‚â´ (colimit.isColimit _).coconePointUniqueUpToIso
      (CommRingCat.pushoutCoconeIsColimit R _ _)
  haveI hel (A : Under R) : pushout.inl _ _ ‚â´ (e A).hom =
      CommRingCat.ofHom Algebra.TensorProduct.includeRight.toRingHom := by
    simpa [e, -IsColimit.comp_coconePointUniqueUpToIso_hom] using
      (colimit.isColimit _).comp_coconePointUniqueUpToIso_hom _ _
  haveI her (A : Under R) : pushout.inr _ _ ‚â´ (e A).hom =
      CommRingCat.ofHom (Algebra.TensorProduct.includeLeft (S := R)).toRingHom := by
    simpa [e, -IsColimit.comp_coconePointUniqueUpToIso_hom] using
      (colimit.isColimit _).comp_coconePointUniqueUpToIso_hom _ _
  refine NatIso.ofComponents (fun A ‚Ü¶
    LinearEquiv.toModuleIso
      { __ := (e A).commRingCatIsoToRingEquiv.toAddEquiv, map_smul' s m := ?_ }) ?_
  ¬∑ dsimp [Iso.commRingCatIsoToRingEquiv, ModuleCat.restrictScalars] at m ‚ä¢
    simp only [Algebra.smul_def, map_mul,
      RingHom.algebraMap_toAlgebra, Under.mk_hom]
    rw [@Algebra.smul_def S (‚ÜëS ‚äó[‚ÜëR] ‚ÜëA.right)]
    change (pushout.inr A.hom œÜ ‚â´ (e A).hom) s * _ =
      (CommRingCat.ofHom (algebraMap S (S ‚äó[R] A.right))) s * _
    congr 3
    simp only [Functor.const_obj_obj, Functor.id_obj, her]
    rfl
  ¬∑ intros X Y f
    ext x
    change (pushout.desc (f.right ‚â´ pushout.inl Y.hom œÜ) (pushout.inr Y.hom œÜ) _ ‚â´ (e Y).hom) _ =
      ((e X).hom ‚â´ CommRingCat.ofHom
        (Algebra.TensorProduct.map (.id R S) (CommRingCat.toAlgHom f))) _
    congr 2
    ext1
    ¬∑ rw [reassoc_of% hel]
      simp [hel, ‚Üê CommRingCat.ofHom_comp, ‚Üê AlgHom.comp_toRingHom]
      rfl
    ¬∑ rw [reassoc_of% her]
      simp [her, ‚Üê CommRingCat.ofHom_comp, ‚Üê AlgHom.comp_toRingHom]

open TensorProduct in
lemma CommRingCat.preservesFiniteLimits_pushout_of_flat
    (hœÜ : œÜ.hom.Flat) : PreservesFiniteLimits (Under.pushout œÜ) := by
  constructor
  intro J _ _
  suffices PreservesLimitsOfShape J (Under.pushout œÜ ‚ãô (commAlgCatEquivUnder S).inverse) from
    preservesLimitsOfShape_of_reflects_of_preserves (G := (commAlgCatEquivUnder _).inverse) _
  suffices PreservesLimitsOfShape J ((Under.pushout œÜ ‚ãô (commAlgCatEquivUnder S).inverse) ‚ãô
      forget‚ÇÇ _ (AlgCat S)) from
    preservesLimitsOfShape_of_reflects_of_preserves  (G := (forget‚ÇÇ _ (AlgCat S))) _
  suffices PreservesLimitsOfShape J (((Under.pushout œÜ ‚ãô (commAlgCatEquivUnder S).inverse) ‚ãô
      forget‚ÇÇ _ (AlgCat S)) ‚ãô forget‚ÇÇ _ (ModuleCat S)) from
    preservesLimitsOfShape_of_reflects_of_preserves
      (G := forget‚ÇÇ _ (ModuleCat S)) _
  have := ModuleCat.preservesFiniteLimits_extendScalars _ hœÜ
  exact preservesLimitsOfShape_of_natIso (pushoutIsoExtendScalars œÜ).symm

def Under.liftCone {J C : Type*} [Category J] [Category C] {F : J ‚•§ C} (c : Cone F) {X : C}
    (f : X ‚ü∂ c.pt) : Cone (Under.lift F ((Functor.const J).map f ‚â´ c.œÄ)) where
  pt := Under.mk f
  œÄ.app j := Under.homMk (c.œÄ.app j)

noncomputable
def Under.isLimitLiftCone
    {J C : Type*} [Category J] [Category C] {F : J ‚•§ C} (c : Cone F) {X : C}
    (f : X ‚ü∂ c.pt) (hc : IsLimit c) : IsLimit (liftCone c f) :=
  isLimitOfReflects (Under.forget _) hc

deriving instance Fintype for CategoryTheory.Pairwise
deriving instance DecidableEq for CategoryTheory.Pairwise.Hom

instance {Œ± : Type*} [Fintype Œ±] [DecidableEq Œ±] :
    FinCategory (CategoryTheory.Pairwise Œ±) where
  fintypeHom
  | .single i, .single j => ‚ü®if h : i = j then {eqToHom (h ‚ñ∏ rfl)} else ‚àÖ, by rintro ‚ü®‚ü©; cat_disch‚ü©
  | .single i, .pair j k => ‚ü®‚àÖ, by rintro ‚ü®‚ü©‚ü©
  | .pair i j, .single k =>
    letI : DecidableEq (Pairwise.pair i j ‚ü∂ Pairwise.single k) := instDecidableEqHom
    ‚ü®(if h : i = k then {CategoryTheory.Pairwise.Hom.left i j ‚â´ eqToHom (h ‚ñ∏ rfl)} else ‚àÖ) ‚à™
      (if h : j = k then {CategoryTheory.Pairwise.Hom.right i j ‚â´ eqToHom (h ‚ñ∏ rfl)} else ‚àÖ),
        by rintro ‚ü®‚ü© <;> cat_disch‚ü©
  | .pair i j, .pair k l =>
    ‚ü®if h : i = k ‚àß j = l then {eqToHom (h.1 ‚ñ∏ h.2 ‚ñ∏ rfl)} else ‚àÖ, by rintro ‚ü®‚ü©; cat_disch‚ü©

namespace AlgebraicGeometry

variable {X Y Z : Scheme.{u}} (f : X ‚ü∂ Y)

/-- Let `X` be a scheme over `R`, `S` an `R`, algebra, and `Y = X √ó_R S`.
This is the canonical map `Œì(X, U) ‚äó[R] S ‚ü∂ Œì(Y, f ‚Åª¬π U)`. -/
noncomputable
def pushoutSectionToSection {R S : CommRingCat} (œÜ : R ‚ü∂ S) {X Y : Scheme} (iX : X ‚ü∂ Spec R)
    (iY : Y ‚ü∂ Spec S) (f : Y ‚ü∂ X) (H : IsPullback f iY iX (Spec.map œÜ))
    (U : X.Opens) :
      pushout ((Scheme.ŒìSpecIso R).inv ‚â´ iX.appLE _ U le_top) œÜ ‚ü∂ Œì(Y, f ‚Åª¬π·µÅ U) :=
  pushout.desc (f.app U) ((Scheme.ŒìSpecIso S).inv ‚â´ iY.appLE _ (f ‚Åª¬π·µÅ U) le_top) <| by
    simp [Scheme.Hom.app_eq_appLE, Scheme.Hom.appLE_comp_appLE, - Scheme.Hom.comp_appLE, H.w]

@[reassoc (attr := simp)]
lemma inl_pushoutSectionToSection {R S : CommRingCat} (œÜ : R ‚ü∂ S) {X Y : Scheme} (iX : X ‚ü∂ Spec R)
    (iY : Y ‚ü∂ Spec S) (f : Y ‚ü∂ X) (H : IsPullback f iY iX (Spec.map œÜ))
    (U : X.Opens) :
      pushout.inl _ _ ‚â´ pushoutSectionToSection _ _ _ _ H U = f.app U :=
  pushout.inl_desc _ _ _

@[reassoc (attr := simp)]
lemma inr_pushoutSectionToSection {R S : CommRingCat} (œÜ : R ‚ü∂ S) {X Y : Scheme} (iX : X ‚ü∂ Spec R)
    (iY : Y ‚ü∂ Spec S) (f : Y ‚ü∂ X) (H : IsPullback f iY iX (Spec.map œÜ))
    (U : X.Opens) :
      pushout.inr _ _ ‚â´ pushoutSectionToSection _ _ _ _ H U =
        (Scheme.ŒìSpecIso S).inv ‚â´ iY.appLE _ (f ‚Åª¬π·µÅ U) le_top :=
  pushout.inr_desc _ _ _

-- -- #check Presheaf.isSheaf_iff_multifork
open TensorProduct
lemma isIso_pushoutSectionToSection_of_isAffineOpen
    {R S : CommRingCat} (œÜ : R ‚ü∂ S) {X Y : Scheme} (iX : X ‚ü∂ Spec R)
    (iY : Y ‚ü∂ Spec S) (f : Y ‚ü∂ X) (H : IsPullback f iY iX (Spec.map œÜ))
    (U : X.Opens) (hU : IsAffineOpen U) :
    IsIso (pushoutSectionToSection _ _ _ _ H U) := by
  refine (IsPushout.isoPushout ?_).isIso_inv
  suffices IsPullback (f.app U).op ((Scheme.ŒìSpecIso S).inv ‚â´ iY.appLE ‚ä§ (f ‚Åª¬π·µÅ U) le_top).op
    ((Scheme.ŒìSpecIso R).inv ‚â´ iX.appLE ‚ä§ U le_top).op œÜ.op from this.flip.unop
  apply IsPullback.of_map Scheme.Spec (by simp [‚Üê op_comp, Scheme.Hom.app_eq_appLE,
    Scheme.Hom.appLE_comp_appLE, - Scheme.Hom.comp_appLE, H.w])
  have : IsAffineHom f := MorphismProperty.of_isPullback H.flip inferInstance
  have : IsPullback (Spec.map (f.app U)) (hU.preimage f).isoSpec.inv hU.isoSpec.inv (f ‚à£_ U) :=
    .of_vert_isIso ‚ü®by simp [‚Üê cancel_mono U.Œπ, Scheme.Hom.app_eq_appLE, hU.preimage f,
      IsAffineOpen.SpecMap_appLE_fromSpec]‚ü©
  convert this.paste_vert ((isPullback_morphismRestrict f U).paste_vert H)
  ¬∑ have : iY.appLE ‚ä§ (f ‚Åª¬π·µÅ U) le_top = iY.appTop ‚â´ Y.presheaf.map (homOfLE le_top).op := rfl
    simpa [‚Üê IsIso.eq_comp_inv, ‚Üê Spec.map_inv, Scheme.toSpecŒì_naturality] using
      congr(Spec.map $this)
  ¬∑ have : iX.appLE ‚ä§ U le_top = iX.appTop ‚â´ X.presheaf.map (homOfLE le_top).op := rfl
    simpa [‚Üê IsIso.eq_comp_inv, ‚Üê Spec.map_inv, Scheme.toSpecŒì_naturality] using
      congr(Spec.map $this)

-- -- #check Presheaf.isSheaf_iff_multifork
open TensorProduct

lemma pushoutSectionToSection_injective_of_isCompact
    {R S : CommRingCat} (œÜ : R ‚ü∂ S) (hœÜ : œÜ.hom.Flat) {X Y : Scheme} (iX : X ‚ü∂ Spec R)
    (iY : Y ‚ü∂ Spec S) (f : Y ‚ü∂ X) (H : IsPullback f iY iX (Spec.map œÜ))
    (U : X.Opens) (hU : IsCompact (X := X) U) :
    Function.Injective (pushoutSectionToSection _ _ _ _ H U) := by
  classical
  obtain ‚ü®I, hI, e‚ü© := isCompact_iff_finite_and_eq_biUnion_affineOpens.mp hU
  have hIE (i : I) : i.1 ‚â§ U := by rw [e]; intro i; aesop
  let (U : X.Opens) : Algebra R Œì(X, U) :=
    ((Scheme.ŒìSpecIso R).inv ‚â´ iX.appLE _ U le_top).hom.toAlgebra
  algebraize [œÜ.hom]
  let œà : Œì(X, U) ‚Üí‚Çê[R] Œ† i : I, Œì(X, i) := Pi.algHom _ _ fun i ‚Ü¶
    ‚ü®(X.presheaf.map (homOfLE (by rw [e]; intro i; aesop)).op).hom, fun r ‚Ü¶ by
      dsimp [RingHom.algebraMap_toAlgebra]
      simp only [‚Üê CommRingCat.comp_apply, Category.assoc, Scheme.Hom.appLE_map]‚ü©
  have hœà : Function.Injective œà := by
    intro s t est
    apply X.IsSheaf.section_ext fun x hx ‚Ü¶ ?_
    simp only [e, Opens.mem_iSup] at hx
    obtain ‚ü®i, hiI, hxi‚ü© := hx
    exact ‚ü®_, _, hxi, congr($est ‚ü®i, hiI‚ü©)‚ü©
  have hœà' : Function.Injective (Algebra.TensorProduct.map (AlgHom.id R S) œà) :=
    Module.Flat.lTensor_preserves_injective_linearMap œà.toLinearMap hœà
  let e‚ÇÅ : pushout ((Scheme.ŒìSpecIso R).inv ‚â´ Scheme.Hom.appLE iX ‚ä§ U le_top) œÜ ‚âÖ
      .of (S ‚äó[R] Œì(X, U)) :=
    (CommRingCat.isPushout_tensorProduct R S Œì(X, U)).flip.isoPushout.symm
  let := hI.fintype
  let e‚ÇÇ : (S ‚äó[R] Œ† i : I, Œì(X, i)) ‚âÉ+* Œ† i : I, Œì(Y, f ‚Åª¬π·µÅ i) :=
    (Algebra.TensorProduct.piRight _ R _ _).toRingEquiv.trans (RingEquiv.piCongrRight fun i ‚Ü¶
      letI := isIso_pushoutSectionToSection_of_isAffineOpen _ _ _ _ H i i.1.2
      (CommRingCat.isPushout_tensorProduct R S _).flip.isoPushout.commRingCatIsoToRingEquiv.trans
      (asIso (pushoutSectionToSection œÜ iX iY f H i)).commRingCatIsoToRingEquiv)
  let œàY : Œì(Y, f ‚Åª¬π·µÅ U) ‚Üí+* Œ† i : I, Œì(Y, f ‚Åª¬π·µÅ i) := Pi.ringHom fun i ‚Ü¶
      (Y.presheaf.map (homOfLE (by rw [e]; intro i; aesop)).op).hom
  refine .of_comp (f := œàY) ?_
  suffices pushoutSectionToSection œÜ iX iY f H U ‚â´ CommRingCat.ofHom œàY = e‚ÇÅ.hom ‚â´
      CommRingCat.ofHom (Algebra.TensorProduct.map (.id R S) œà).toRingHom ‚â´
      CommRingCat.ofHom e‚ÇÇ.toRingHom by
    convert (e‚ÇÇ.injective.comp hœà').comp e‚ÇÅ.commRingCatIsoToRingEquiv.injective
    exact congr($this)
  ext1
  ¬∑ suffices Scheme.Hom.app f U ‚â´ CommRingCat.ofHom œàY = CommRingCat.ofHom
        (e‚ÇÇ.toRingHom.comp (Algebra.TensorProduct.includeRight.comp œà).toRingHom) by
      simpa [e‚ÇÅ, ‚Üê CommRingCat.ofHom_comp, RingHom.comp_assoc, ‚Üê AlgHom.comp_toRingHom]
    ext x i
    trans (X.presheaf.map (homOfLE (hIE _)).op ‚â´
        CommRingCat.ofHom Algebra.TensorProduct.includeRight.toRingHom ‚â´
        (CommRingCat.isPushout_tensorProduct ‚ÜëR ‚ÜëS ‚ÜëŒì(X, i.1)).flip.isoPushout.hom ‚â´
        pushoutSectionToSection œÜ iX iY f H i.1) x
    ¬∑ change (Scheme.Hom.app f U ‚â´
        Y.presheaf.map (homOfLE (f.preimage_mono (hIE _))).op) x = _
      congr 2
      simp [RingHom.algebraMap_toAlgebra]
    ¬∑ simp [e‚ÇÇ, Iso.commRingCatIsoToRingEquiv, -IsPushout.inl_isoPushout_hom_assoc]; rfl
  ¬∑ suffices (Scheme.ŒìSpecIso S).inv ‚â´ iY.appLE ‚ä§ (f ‚Åª¬π·µÅ U) le_top ‚â´ CommRingCat.ofHom œàY =
        CommRingCat.ofHom (e‚ÇÇ.toRingHom.comp Algebra.TensorProduct.includeLeftRingHom) by
      simpa [e‚ÇÅ, ‚Üê CommRingCat.ofHom_comp, RingHom.comp_assoc,
        ‚Üê AlgHom.comp_toRingHom, show Algebra.TensorProduct.includeLeftRingHom =
          (Algebra.TensorProduct.includeLeft (S := R)).toRingHom from rfl]
    ext x i
    trans (CommRingCat.ofHom Algebra.TensorProduct.includeLeftRingHom ‚â´
        (CommRingCat.isPushout_tensorProduct ‚ÜëR ‚ÜëS ‚ÜëŒì(X, i.1)).flip.isoPushout.hom ‚â´
        pushoutSectionToSection œÜ iX iY f H i.1) x
    ¬∑ change ((Scheme.ŒìSpecIso S).inv ‚â´ iY.appLE ‚ä§ (f ‚Åª¬π·µÅ U) le_top ‚â´
        Y.presheaf.map (homOfLE (f.preimage_mono (hIE _))).op) x = _
      congr 2
      simpa [-inr_pushoutSectionToSection] using (inr_pushoutSectionToSection ..).symm
    ¬∑ simp [e‚ÇÇ, -IsPushout.inr_isoPushout_hom_assoc]; rfl

lemma isIso_pushoutSectionToSection_of_isQuasiSeparated
    {R S : CommRingCat} (œÜ : R ‚ü∂ S) (hœÜ : œÜ.hom.Flat) {X Y : Scheme} (iX : X ‚ü∂ Spec R)
    (iY : Y ‚ü∂ Spec S) (f : Y ‚ü∂ X) (H : IsPullback f iY iX (Spec.map œÜ))
    (U : X.Opens) (hU : IsCompact (X := X) U) (hU' : IsQuasiSeparated (Œ± := X) U) :
    IsIso (pushoutSectionToSection _ _ _ _ H U) := by
  classical
  obtain ‚ü®s, hs, e‚ü© := isCompact_iff_finite_and_eq_biUnion_affineOpens.mp hU
  have hsU (i : s) : i.1 ‚â§ U := by rw [e]; intro i; aesop
  let D := Pairwise.diagram fun i : s ‚Ü¶ i.1.1
  let iXU : R ‚ü∂ Œì(X, U) :=
    (Scheme.ŒìSpecIso R).inv ‚â´ iX.appTop ‚â´ X.presheaf.map (homOfLE le_top).op
  have h : iSup D.obj = U := by
    refine le_antisymm (iSup_le_iff.mpr ?_)
      (e.trans_le (iSup‚ÇÇ_le_iff.mpr fun i hi ‚Ü¶ le_iSup D.obj (.single ‚ü®i, hi‚ü©)))
    rintro (i | ‚ü®i, j‚ü©)
    exacts [hsU _, inf_le_left.trans (hsU i)]
  let c‚ÇÄ : Cocone (Pairwise.diagram fun i : s ‚Ü¶ (i : X.Opens)) := (colimit.cocone _).extend
    (eqToIso (Y := U) (by simpa [CompleteLattice.colimit_eq_iSup])).hom
  let F := Under.lift _ ((Functor.const _).map iXU ‚â´ ((X.presheaf.mapCone c‚ÇÄ.op).œÄ)) ‚ãô
      Under.pushout œÜ ‚ãô Under.forget S
  let Œ±F : F ‚ü∂ D.op ‚ãô (Opens.map f.base).op ‚ãô Y.presheaf :=
  { app _ := (pushout.congrHom (by simp [iXU, Scheme.Hom.app_eq_appLE]) rfl).hom ‚â´
      pushoutSectionToSection _ _ _ _ H _  }
  let c : Cone F := (Under.pushout œÜ ‚ãô Under.forget S).mapCone
    (Under.liftCone (X.presheaf.mapCone c‚ÇÄ.op) iXU)
  have := CommRingCat.preservesFiniteLimits_pushout_of_flat _ hœÜ
  let : Fintype s := hs.fintype
  let hc : IsLimit c :=
    haveI HX := ((TopCat.Presheaf.isSheaf_iff_isSheafPreservesLimitPairwiseIntersections
      _).mp X.IsSheaf (fun i : s ‚Ü¶ i)).preserves (c := c‚ÇÄ.op)
    haveI HX := (HX (IsColimit.extendIso _ (colimit.isColimit _)).op).some
    isLimitOfPreserves (Under.pushout œÜ ‚ãô Under.forget _) (Under.isLimitLiftCone _ iXU HX)
  let c'‚ÇÄ : Cocone (D ‚ãô Opens.map f.base) := (colimit.cocone _).extend
    (eqToIso (Y := f ‚Åª¬π·µÅ U) (by
      simp only [colimit.cocone_x, CompleteLattice.colimit_eq_iSup, ‚Üê h, Scheme.Hom.preimage_iSup]
      rfl)).hom
  let c' : Cone (D.op ‚ãô (Opens.map f.base).op ‚ãô Y.presheaf) := Y.presheaf.mapCone c'‚ÇÄ.op
  let hc' : IsLimit c' :=
    letI e : D ‚ãô Opens.map f.base ‚âÖ Pairwise.diagram fun i : s ‚Ü¶ f ‚Åª¬π·µÅ i :=
      NatIso.ofComponents (fun | .single i => .refl _ | .pair i j => .refl _)
    haveI HX := ((TopCat.Presheaf.isSheaf_iff_isSheafPreservesLimitPairwiseIntersections _).mp
      Y.IsSheaf (fun i : s ‚Ü¶ f ‚Åª¬π·µÅ i)).preserves (c := ((Cocones.precompose e.inv).obj c'‚ÇÄ).op)
    (IsLimit.postcomposeHomEquiv (Functor.isoWhiskerRight (NatIso.op e.symm) Y.presheaf) _)
      ((HX ((IsColimit.precomposeInvEquiv e _).symm
        (IsColimit.extendIso _ (colimit.isColimit _))).op).some.ofIsoLimit (Cones.ext (.refl _)))
  have HŒ±F‚ÇÅ (i : _) : IsIso (Œ±F.app (.op <| .single i)) := by
    dsimp [Œ±F]
    have := isIso_pushoutSectionToSection_of_isAffineOpen _ _ _ _ H i i.1.2
    infer_instance
  have HŒ±F‚ÇÇ (i j : _) : Mono (Œ±F.app (.op <| .pair i j)) := by
    dsimp [Œ±F]
    have := pushoutSectionToSection_injective_of_isCompact _ hœÜ _ _ _ H (i ‚äì j)
      (hU' _ _ (hsU _) i.1.1.2 i.1.2.isCompact (hsU _) j.1.1.2 j.1.2.isCompact)
    rw [‚Üê ConcreteCategory.mono_iff_injective_of_preservesPullback] at this
    infer_instance
  let f‚ÇÅ : c.pt ‚ü∂ c'.pt := hc'.lift ((Cones.postcompose Œ±F).obj c)
  let f‚ÇÇ : c'.pt ‚ü∂ c.pt := hc.lift ‚ü®c'.pt, ‚ü®fun
    | .op (.single i) => c'.œÄ.app _ ‚â´ inv (Œ±F.app (.op <| .single i))
    | .op (.pair i j) => c'.œÄ.app (.op (.single i)) ‚â´ inv (Œ±F.app (.op <| .single i)) ‚â´
        F.map (Quiver.Hom.op <| Pairwise.Hom.left i j), by
    rintro ‚ü®i‚ü© ‚ü®j‚ü© f
    obtain ‚ü®i | ‚ü®i, j‚ü© | ‚ü®i, j‚ü© | ‚ü®i, j‚ü©, rfl‚ü© :=
      (show Function.Surjective Quiver.Hom.op from Quiver.Hom.opEquiv.surjective) f
    ¬∑ simp [show Pairwise.Hom.id_single i = ùüô (Pairwise.single i) from rfl]
    ¬∑ simp [show Pairwise.Hom.id_pair i j = ùüô (Pairwise.pair i j) from rfl]
    ¬∑ simp
    ¬∑ rw [‚Üê cancel_mono (Œ±F.app _)]
      simpa using (c'.w (Quiver.Hom.op <| Pairwise.Hom.left i j)).trans
        (c'.w (Quiver.Hom.op <| Pairwise.Hom.right i j)).symm‚ü©‚ü©
  let e : c.pt ‚âÖ c'.pt := by
    refine ‚ü®f‚ÇÅ, f‚ÇÇ, hc.hom_ext ?_, hc'.hom_ext ?_‚ü©
    ¬∑ rintro ‚ü®i | ‚ü®i, j‚ü©‚ü© <;> simp [f‚ÇÅ, f‚ÇÇ]
    ¬∑ rintro ‚ü®i | ‚ü®i, j‚ü©‚ü©
      ¬∑ simp [f‚ÇÅ, f‚ÇÇ]
      ¬∑ simpa [f‚ÇÅ, f‚ÇÇ] using c'.w (Quiver.Hom.op <| Pairwise.Hom.left i j)
  convert e.isIso_hom using 1
  ¬∑ refine hc'.hom_ext fun i ‚Ü¶ ?_
    rw [hc'.fac]
    ext1
    ¬∑ simp [Œ±F, c, @pushout.inl_desc_assoc, Under.liftCone]; rfl
    ¬∑ simp [Œ±F, c, @pushout.inr_desc_assoc, c']; rfl

attribute [gcongr] Scheme.Hom.preimage_mono

/-- Given `Y = X √ó‚Çõ T` with `U‚Çú` an affine open subset of `T` and `U‚Çì` a qcqs subset of `X`.
Suppose that `T` is flat over `S`, and `U‚Çú` and `U‚Çì` are contained in a common affine open `U‚Çõ ‚äÜ S`,
then `Œì(Y, pr‚Çú ‚Åª¬π U‚Çú ‚à© pr‚Çì ‚Åª¬π U‚Çì) = Œì(T, U‚Çú) ‚äó[Œì(S, U‚Çõ)] Œì(X, U‚Çì)`. -/
lemma isIso_pushoutDesc_appLE_appLE_of_isCompact_of_isQuasiSeparated_of_flat
    {X Y S T : Scheme} {f : T ‚ü∂ S} {g : Y ‚ü∂ X} {iX : X ‚ü∂ S} {iY : Y ‚ü∂ T}
    (H : IsPullback g iY iX f) [Flat f]
    {US : S.Opens} (hUS : IsAffineOpen US) {UT : T.Opens} (hUT : IsAffineOpen UT)
    {UX : X.Opens} (hUST : UT ‚â§ f ‚Åª¬π·µÅ US) (hUSX : UX ‚â§ iX ‚Åª¬π·µÅ US)
    {UY : Y.Opens} (hUY : g ‚Åª¬π·µÅ UX ‚äì iY ‚Åª¬π·µÅ UT = UY)
    (hUX : IsCompact (X := X) UX) (hUX' : IsQuasiSeparated (Œ± := X) UX) :
    IsIso (pushout.desc (g.appLE UX UY (by simp [‚Üê hUY])) (iY.appLE UT UY (by simp [‚Üê hUY]))
      (by simp only [Scheme.Hom.appLE_comp_appLE, H.w]) :
      pushout (iX.appLE US UX hUSX) (f.appLE US UT hUST) ‚ü∂ Œì(Y, UY)) := by
  have h‚ÇÅ : IsPullback (g ‚à£_ UX) (iY.resLE (f ‚Åª¬π·µÅ US) (g ‚Åª¬π·µÅ UX)
      (by rw [‚Üê Scheme.Hom.comp_preimage, ‚Üê H.w]; exact g.preimage_mono hUSX))
      (iX.resLE _ _ hUSX) (f ‚à£_ US) := by
    refine .of_bot ?_ ?_ (isPullback_morphismRestrict f US)
    ¬∑ simpa using (isPullback_morphismRestrict g UX).paste_vert H
    ¬∑ simp [‚Üê cancel_mono US.Œπ, H.w]
  have h‚ÇÇ : IsPullback (Scheme.homOfLE _ (by simp [‚Üê hUY])) (iY.resLE UT UY (by simp [‚Üê hUY]))
      ((iY.resLE (f ‚Åª¬π·µÅ US) (g ‚Åª¬π·µÅ UX)
      (by rw [‚Üê Scheme.Hom.comp_preimage, ‚Üê H.w]; exact g.preimage_mono hUSX)))
      (Scheme.homOfLE _ hUST) := by
    refine (IsOpenImmersion.isPullback _ _ _ _ (by simp) ?_).flip
    simp only [Scheme.opensRange_homOfLE,
      ‚Üê Scheme.Hom.comp_preimage, Scheme.Hom.resLE_comp_Œπ]
    rw [Scheme.Hom.comp_preimage, ‚Üê (g ‚Åª¬π·µÅ UX).Œπ.image_injective.eq_iff]
    simp only [Scheme.Hom.image_preimage_eq_opensRange_inf, Scheme.Opens.opensRange_Œπ]
    simp [‚Üê hUY]
  have h‚ÇÉ : IsPullback (f.resLE US UT hUST) hUT.isoSpec.hom hUS.isoSpec.hom
    (Spec.map (f.appLE _ _ hUST)) := .of_vert_isIso ‚ü®by simp [IsAffineOpen.isoSpec_hom]‚ü©
  have H := (h‚ÇÇ.paste_horiz h‚ÇÅ).paste_vert h‚ÇÉ
  simp only [‚Üê Scheme.Hom.resLE_eq_morphismRestrict, Scheme.Hom.map_resLE] at H
  have inst : CompactSpace UX := isCompact_iff_compactSpace.mp hUX
  have inst : QuasiSeparatedSpace UX := (isQuasiSeparated_iff_quasiSeparatedSpace _ UX.2).mp hUX'
  have := isIso_pushoutSectionToSection_of_isQuasiSeparated _
    (by exact Flat.flat_of_affine_subset ‚ü®_, hUS‚ü© ‚ü®_, hUT‚ü© hUST) _ _ _ H ‚ä§ isCompact_univ
    isQuasiSeparated_univ
  have : IsIso (pushout.map (iX.appLE US UX hUSX) (f.appLE US UT hUST) _ _
    (X.presheaf.map (eqToHom (show UX.Œπ ''·µÅ ‚ä§ = UX by simp)).op) (ùüô _) (ùüô _)
      (by simp [IsAffineOpen.isoSpec_hom_appTop, Scheme.Hom.resLE_appLE]) (by simp) ‚â´
      pushoutSectionToSection (f.appLE US UT hUST)
      (iX.resLE US UX hUSX ‚â´ hUS.isoSpec.hom)
      (iY.resLE UT UY (by simp [‚Üê hUY]) ‚â´ hUT.isoSpec.hom)
      (g.resLE UX UY (by simp [‚Üê hUY])) H ‚ä§ ‚â´
      Y.presheaf.map (eqToHom (show UY = UY.Œπ ''·µÅ ‚ä§ by simp)).op) :=
    inferInstance
  convert this
  ext1
  ¬∑ simp [Scheme.Hom.app_eq_appLE, Scheme.Hom.resLE_appLE]
  ¬∑ simp [IsAffineOpen.isoSpec_hom_appTop, Scheme.Hom.resLE_appLE]

end AlgebraicGeometry
