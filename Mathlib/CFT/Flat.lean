module

public import Mathlib.AlgebraicGeometry.Morphisms.Flat
public import Mathlib.Algebra.Category.Ring.Under.Limits

@[expose] public section

open CategoryTheory CategoryTheory.Limits Opposite TopologicalSpace

universe v u

variable {R S : CommRingCat.{u}} (Ï† : R âŸ¶ S) (hÏ† : Ï†.hom.Flat)

namespace AlgebraicGeometry

variable {X Y Z : Scheme.{u}} (f : X âŸ¶ Y)

/-- Let `X` be a scheme over `R`, `S` an `R`, algebra, and `Y = X Ã—_R S`.
This is the canonical map `Î“(X, U) âŠ—[R] S âŸ¶ Î“(Y, f â»Â¹ U)`. -/
noncomputable
def pushoutSectionToSection {R S : CommRingCat} (Ï† : R âŸ¶ S) {X Y : Scheme} (iX : X âŸ¶ Spec R)
    (iY : Y âŸ¶ Spec S) (f : Y âŸ¶ X) (H : IsPullback f iY iX (Spec.map Ï†))
    (U : X.Opens) :
      pushout ((Scheme.Î“SpecIso R).inv â‰« iX.appLE _ U le_top) Ï† âŸ¶ Î“(Y, f â»Â¹áµ U) :=
  pushout.desc (f.app U) ((Scheme.Î“SpecIso S).inv â‰« iY.appLE _ (f â»Â¹áµ U) le_top) <| by
    simp [Scheme.Hom.app_eq_appLE, Scheme.Hom.appLE_comp_appLE, - Scheme.Hom.comp_appLE, H.w]

@[reassoc (attr := simp)]
lemma inl_pushoutSectionToSection {R S : CommRingCat} (Ï† : R âŸ¶ S) {X Y : Scheme} (iX : X âŸ¶ Spec R)
    (iY : Y âŸ¶ Spec S) (f : Y âŸ¶ X) (H : IsPullback f iY iX (Spec.map Ï†))
    (U : X.Opens) :
      pushout.inl _ _ â‰« pushoutSectionToSection _ _ _ _ H U = f.app U :=
  pushout.inl_desc _ _ _

@[reassoc (attr := simp)]
lemma inr_pushoutSectionToSection {R S : CommRingCat} (Ï† : R âŸ¶ S) {X Y : Scheme} (iX : X âŸ¶ Spec R)
    (iY : Y âŸ¶ Spec S) (f : Y âŸ¶ X) (H : IsPullback f iY iX (Spec.map Ï†))
    (U : X.Opens) :
      pushout.inr _ _ â‰« pushoutSectionToSection _ _ _ _ H U =
        (Scheme.Î“SpecIso S).inv â‰« iY.appLE _ (f â»Â¹áµ U) le_top :=
  pushout.inr_desc _ _ _

-- -- #check Presheaf.isSheaf_iff_multifork
open TensorProduct
lemma isIso_pushoutSectionToSection_of_isAffineOpen
    {R S : CommRingCat} (Ï† : R âŸ¶ S) {X Y : Scheme} (iX : X âŸ¶ Spec R)
    (iY : Y âŸ¶ Spec S) (f : Y âŸ¶ X) (H : IsPullback f iY iX (Spec.map Ï†))
    (U : X.Opens) (hU : IsAffineOpen U) :
    IsIso (pushoutSectionToSection _ _ _ _ H U) := by
  refine (IsPushout.isoPushout ?_).isIso_inv
  suffices IsPullback (f.app U).op ((Scheme.Î“SpecIso S).inv â‰« iY.appLE âŠ¤ (f â»Â¹áµ U) le_top).op
    ((Scheme.Î“SpecIso R).inv â‰« iX.appLE âŠ¤ U le_top).op Ï†.op from this.flip.unop
  apply IsPullback.of_map Scheme.Spec (by simp [â† op_comp, Scheme.Hom.app_eq_appLE,
    Scheme.Hom.appLE_comp_appLE, - Scheme.Hom.comp_appLE, H.w])
  have : IsAffineHom f := MorphismProperty.of_isPullback H.flip inferInstance
  have : IsPullback (Spec.map (f.app U)) (hU.preimage f).isoSpec.inv hU.isoSpec.inv (f âˆ£_ U) :=
    .of_vert_isIso âŸ¨by simp [â† cancel_mono U.Î¹, Scheme.Hom.app_eq_appLE, hU.preimage f,
      IsAffineOpen.SpecMap_appLE_fromSpec]âŸ©
  convert this.paste_vert ((isPullback_morphismRestrict f U).paste_vert H)
  Â· have : iY.appLE âŠ¤ (f â»Â¹áµ U) le_top = iY.appTop â‰« Y.presheaf.map (homOfLE le_top).op := rfl
    simpa [â† IsIso.eq_comp_inv, â† Spec.map_inv, Scheme.toSpecÎ“_naturality] using
      congr(Spec.map $this)
  Â· have : iX.appLE âŠ¤ U le_top = iX.appTop â‰« X.presheaf.map (homOfLE le_top).op := rfl
    simpa [â† IsIso.eq_comp_inv, â† Spec.map_inv, Scheme.toSpecÎ“_naturality] using
      congr(Spec.map $this)

open TensorProduct

lemma pushoutSectionToSection_injective_of_isCompact
    {R S : CommRingCat} (Ï† : R âŸ¶ S) (hÏ† : Ï†.hom.Flat) {X Y : Scheme} (iX : X âŸ¶ Spec R)
    (iY : Y âŸ¶ Spec S) (f : Y âŸ¶ X) (H : IsPullback f iY iX (Spec.map Ï†))
    (U : X.Opens) (hU : IsCompact (X := X) U) :
    Function.Injective (pushoutSectionToSection _ _ _ _ H U) := by
  classical
  obtain âŸ¨I, hI, eâŸ© := isCompact_iff_finite_and_eq_biUnion_affineOpens.mp hU
  have hIE (i : I) : i.1 â‰¤ U := by rw [e]; intro i; aesop
  let (U : X.Opens) : Algebra R Î“(X, U) :=
    ((Scheme.Î“SpecIso R).inv â‰« iX.appLE _ U le_top).hom.toAlgebra
  algebraize [Ï†.hom]
  let Ïˆ : Î“(X, U) â†’â‚[R] Î  i : I, Î“(X, i) := Pi.algHom _ _ fun i â†¦
    âŸ¨(X.presheaf.map (homOfLE (by rw [e]; intro i; aesop)).op).hom, fun r â†¦ by
      dsimp [RingHom.algebraMap_toAlgebra]
      simp only [â† CommRingCat.comp_apply, Category.assoc, Scheme.Hom.appLE_map]âŸ©
  have hÏˆ : Function.Injective Ïˆ := by
    intro s t est
    apply X.IsSheaf.section_ext fun x hx â†¦ ?_
    simp only [e, Opens.mem_iSup] at hx
    obtain âŸ¨i, hiI, hxiâŸ© := hx
    exact âŸ¨_, _, hxi, congr($est âŸ¨i, hiIâŸ©)âŸ©
  have hÏˆ' : Function.Injective (Algebra.TensorProduct.map (AlgHom.id R S) Ïˆ) :=
    Module.Flat.lTensor_preserves_injective_linearMap Ïˆ.toLinearMap hÏˆ
  let eâ‚ : pushout ((Scheme.Î“SpecIso R).inv â‰« Scheme.Hom.appLE iX âŠ¤ U le_top) Ï† â‰…
      .of (S âŠ—[R] Î“(X, U)) :=
    (CommRingCat.isPushout_tensorProduct R S Î“(X, U)).flip.isoPushout.symm
  let := hI.fintype
  let eâ‚‚ : (S âŠ—[R] Î  i : I, Î“(X, i)) â‰ƒ+* Î  i : I, Î“(Y, f â»Â¹áµ i) :=
    (Algebra.TensorProduct.piRight _ R _ _).toRingEquiv.trans (RingEquiv.piCongrRight fun i â†¦
      letI := isIso_pushoutSectionToSection_of_isAffineOpen _ _ _ _ H i i.1.2
      (CommRingCat.isPushout_tensorProduct R S _).flip.isoPushout.commRingCatIsoToRingEquiv.trans
      (asIso (pushoutSectionToSection Ï† iX iY f H i)).commRingCatIsoToRingEquiv)
  let ÏˆY : Î“(Y, f â»Â¹áµ U) â†’+* Î  i : I, Î“(Y, f â»Â¹áµ i) := Pi.ringHom fun i â†¦
      (Y.presheaf.map (homOfLE (by rw [e]; intro i; aesop)).op).hom
  refine .of_comp (f := ÏˆY) ?_
  suffices pushoutSectionToSection Ï† iX iY f H U â‰« CommRingCat.ofHom ÏˆY = eâ‚.hom â‰«
      CommRingCat.ofHom (Algebra.TensorProduct.map (.id R S) Ïˆ).toRingHom â‰«
      CommRingCat.ofHom eâ‚‚.toRingHom by
    convert (eâ‚‚.injective.comp hÏˆ').comp eâ‚.commRingCatIsoToRingEquiv.injective
    exact congr($this)
  ext1
  Â· suffices Scheme.Hom.app f U â‰« CommRingCat.ofHom ÏˆY = CommRingCat.ofHom
        (eâ‚‚.toRingHom.comp (Algebra.TensorProduct.includeRight.comp Ïˆ).toRingHom) by
      simpa [eâ‚, â† CommRingCat.ofHom_comp, RingHom.comp_assoc, â† AlgHom.comp_toRingHom]
    ext x i
    trans (X.presheaf.map (homOfLE (hIE _)).op â‰«
        CommRingCat.ofHom Algebra.TensorProduct.includeRight.toRingHom â‰«
        (CommRingCat.isPushout_tensorProduct â†‘R â†‘S â†‘Î“(X, i.1)).flip.isoPushout.hom â‰«
        pushoutSectionToSection Ï† iX iY f H i.1) x
    Â· change (Scheme.Hom.app f U â‰«
        Y.presheaf.map (homOfLE (f.preimage_mono (hIE _))).op) x = _
      congr 2
      simp [RingHom.algebraMap_toAlgebra]
    Â· simp [eâ‚‚, Iso.commRingCatIsoToRingEquiv, -IsPushout.inl_isoPushout_hom_assoc]; rfl
  Â· suffices (Scheme.Î“SpecIso S).inv â‰« iY.appLE âŠ¤ (f â»Â¹áµ U) le_top â‰« CommRingCat.ofHom ÏˆY =
        CommRingCat.ofHom (eâ‚‚.toRingHom.comp Algebra.TensorProduct.includeLeftRingHom) by
      simpa [eâ‚, â† CommRingCat.ofHom_comp, RingHom.comp_assoc,
        â† AlgHom.comp_toRingHom, show Algebra.TensorProduct.includeLeftRingHom =
          (Algebra.TensorProduct.includeLeft (S := R)).toRingHom from rfl]
    ext x i
    trans (CommRingCat.ofHom Algebra.TensorProduct.includeLeftRingHom â‰«
        (CommRingCat.isPushout_tensorProduct â†‘R â†‘S â†‘Î“(X, i.1)).flip.isoPushout.hom â‰«
        pushoutSectionToSection Ï† iX iY f H i.1) x
    Â· change ((Scheme.Î“SpecIso S).inv â‰« iY.appLE âŠ¤ (f â»Â¹áµ U) le_top â‰«
        Y.presheaf.map (homOfLE (f.preimage_mono (hIE _))).op) x = _
      congr 2
      simpa [-inr_pushoutSectionToSection] using (inr_pushoutSectionToSection ..).symm
    Â· simp [eâ‚‚, -IsPushout.inr_isoPushout_hom_assoc]; rfl

lemma isIso_pushoutSectionToSection_of_isQuasiSeparated
    {R S : CommRingCat} (Ï† : R âŸ¶ S) (hÏ† : Ï†.hom.Flat) {X Y : Scheme} (iX : X âŸ¶ Spec R)
    (iY : Y âŸ¶ Spec S) (f : Y âŸ¶ X) (H : IsPullback f iY iX (Spec.map Ï†))
    (U : X.Opens) (hU : IsCompact (X := X) U) (hU' : IsQuasiSeparated (Î± := X) U) :
    IsIso (pushoutSectionToSection _ _ _ _ H U) := by
  classical
  obtain âŸ¨s, hs, eâŸ© := isCompact_iff_finite_and_eq_biUnion_affineOpens.mp hU
  have hsU (i : s) : i.1 â‰¤ U := by rw [e]; intro i; aesop
  let D := Pairwise.diagram fun i : s â†¦ i.1.1
  let iXU : R âŸ¶ Î“(X, U) :=
    (Scheme.Î“SpecIso R).inv â‰« iX.appTop â‰« X.presheaf.map (homOfLE le_top).op
  have h : iSup D.obj = U := by
    refine le_antisymm (iSup_le_iff.mpr ?_)
      (e.trans_le (iSupâ‚‚_le_iff.mpr fun i hi â†¦ le_iSup D.obj (.single âŸ¨i, hiâŸ©)))
    rintro (i | âŸ¨i, jâŸ©)
    exacts [hsU _, inf_le_left.trans (hsU i)]
  let câ‚€ : Cocone (Pairwise.diagram fun i : s â†¦ (i : X.Opens)) := (colimit.cocone _).extend
    (eqToIso (Y := U) (by simpa [CompleteLattice.colimit_eq_iSup])).hom
  let F := Under.lift _ ((Functor.const _).map iXU â‰« ((X.presheaf.mapCone câ‚€.op).Ï€)) â‹™
      Under.pushout Ï† â‹™ Under.forget S
  let Î±F : F âŸ¶ D.op â‹™ (Opens.map f.base).op â‹™ Y.presheaf :=
  { app _ := (pushout.congrHom (by simp [iXU, Scheme.Hom.app_eq_appLE]) rfl).hom â‰«
      pushoutSectionToSection _ _ _ _ H _  }
  let c : Cone F := (Under.pushout Ï† â‹™ Under.forget S).mapCone
    (Under.liftCone (X.presheaf.mapCone câ‚€.op) iXU)
  have := CommRingCat.Under.preservesFiniteLimits_of_flat _ hÏ†
  let : Fintype s := hs.fintype
  let hc : IsLimit c :=
    haveI HX := ((TopCat.Presheaf.isSheaf_iff_isSheafPreservesLimitPairwiseIntersections
      _).mp X.IsSheaf (fun i : s â†¦ i)).preserves (c := câ‚€.op)
    haveI HX := (HX (IsColimit.extendIso _ (colimit.isColimit _)).op).some
    isLimitOfPreserves (Under.pushout Ï† â‹™ Under.forget _) (Under.isLimitLiftCone _ iXU HX)
  let c'â‚€ : Cocone (D â‹™ Opens.map f.base) := (colimit.cocone _).extend
    (eqToIso (Y := f â»Â¹áµ U) (by
      simp only [colimit.cocone_x, CompleteLattice.colimit_eq_iSup, â† h, Scheme.Hom.preimage_iSup]
      rfl)).hom
  let c' : Cone (D.op â‹™ (Opens.map f.base).op â‹™ Y.presheaf) := Y.presheaf.mapCone c'â‚€.op
  let hc' : IsLimit c' :=
    letI e : D â‹™ Opens.map f.base â‰… Pairwise.diagram fun i : s â†¦ f â»Â¹áµ i :=
      NatIso.ofComponents (fun | .single i => .refl _ | .pair i j => .refl _)
    haveI HX := ((TopCat.Presheaf.isSheaf_iff_isSheafPreservesLimitPairwiseIntersections _).mp
      Y.IsSheaf (fun i : s â†¦ f â»Â¹áµ i)).preserves (c := ((Cocones.precompose e.inv).obj c'â‚€).op)
    (IsLimit.postcomposeHomEquiv (Functor.isoWhiskerRight (NatIso.op e.symm) Y.presheaf) _)
      ((HX ((IsColimit.precomposeInvEquiv e _).symm
        (IsColimit.extendIso _ (colimit.isColimit _))).op).some.ofIsoLimit (Cones.ext (.refl _)))
  have HÎ±Fâ‚ (i : _) : IsIso (Î±F.app (.op <| .single i)) := by
    dsimp [Î±F]
    have := isIso_pushoutSectionToSection_of_isAffineOpen _ _ _ _ H i i.1.2
    infer_instance
  have HÎ±Fâ‚‚ (i j : _) : Mono (Î±F.app (.op <| .pair i j)) := by
    dsimp [Î±F]
    have := pushoutSectionToSection_injective_of_isCompact _ hÏ† _ _ _ H (i âŠ“ j)
      (hU' _ _ (hsU _) i.1.1.2 i.1.2.isCompact (hsU _) j.1.1.2 j.1.2.isCompact)
    rw [â† ConcreteCategory.mono_iff_injective_of_preservesPullback] at this
    infer_instance
  let fâ‚ : c.pt âŸ¶ c'.pt := hc'.lift ((Cones.postcompose Î±F).obj c)
  let fâ‚‚ : c'.pt âŸ¶ c.pt := hc.lift âŸ¨c'.pt, âŸ¨fun
    | .op (.single i) => c'.Ï€.app _ â‰« inv (Î±F.app (.op <| .single i))
    | .op (.pair i j) => c'.Ï€.app (.op (.single i)) â‰« inv (Î±F.app (.op <| .single i)) â‰«
        F.map (Quiver.Hom.op <| Pairwise.Hom.left i j), by
    rintro âŸ¨iâŸ© âŸ¨jâŸ© f
    obtain âŸ¨i | âŸ¨i, jâŸ© | âŸ¨i, jâŸ© | âŸ¨i, jâŸ©, rflâŸ© :=
      (show Function.Surjective Quiver.Hom.op from Quiver.Hom.opEquiv.surjective) f
    Â· simp [show Pairwise.Hom.id_single i = ğŸ™ (Pairwise.single i) from rfl]
    Â· simp [show Pairwise.Hom.id_pair i j = ğŸ™ (Pairwise.pair i j) from rfl]
    Â· simp
    Â· rw [â† cancel_mono (Î±F.app _)]
      simpa using (c'.w (Quiver.Hom.op <| Pairwise.Hom.left i j)).trans
        (c'.w (Quiver.Hom.op <| Pairwise.Hom.right i j)).symmâŸ©âŸ©
  let e : c.pt â‰… c'.pt := by
    refine âŸ¨fâ‚, fâ‚‚, hc.hom_ext ?_, hc'.hom_ext ?_âŸ©
    Â· rintro âŸ¨i | âŸ¨i, jâŸ©âŸ© <;> simp [fâ‚, fâ‚‚]
    Â· rintro âŸ¨i | âŸ¨i, jâŸ©âŸ©
      Â· simp [fâ‚, fâ‚‚]
      Â· simpa [fâ‚, fâ‚‚] using c'.w (Quiver.Hom.op <| Pairwise.Hom.left i j)
  convert e.isIso_hom using 1
  Â· refine hc'.hom_ext fun i â†¦ ?_
    rw [hc'.fac]
    ext1
    Â· simp [Î±F, c, @pushout.inl_desc_assoc, Under.liftCone]; rfl
    Â· simp [Î±F, c, @pushout.inr_desc_assoc, c']; rfl

attribute [gcongr] Scheme.Hom.preimage_mono

/-- Given `Y = X Ã—â‚› T` with `Uâ‚œ` an affine open subset of `T` and `Uâ‚“` a qcqs subset of `X`.
Suppose that `T` is flat over `S`, and `Uâ‚œ` and `Uâ‚“` are contained in a common affine open `Uâ‚› âŠ† S`,
then `Î“(Y, prâ‚œ â»Â¹ Uâ‚œ âˆ© prâ‚“ â»Â¹ Uâ‚“) = Î“(T, Uâ‚œ) âŠ—[Î“(S, Uâ‚›)] Î“(X, Uâ‚“)`. -/
lemma isIso_pushoutDesc_appLE_appLE_of_isCompact_of_isQuasiSeparated_of_flat
    {X Y S T : Scheme} {f : T âŸ¶ S} {g : Y âŸ¶ X} {iX : X âŸ¶ S} {iY : Y âŸ¶ T}
    (H : IsPullback g iY iX f) [Flat f]
    {US : S.Opens} (hUS : IsAffineOpen US) {UT : T.Opens} (hUT : IsAffineOpen UT)
    {UX : X.Opens} (hUST : UT â‰¤ f â»Â¹áµ US) (hUSX : UX â‰¤ iX â»Â¹áµ US)
    {UY : Y.Opens} (hUY : g â»Â¹áµ UX âŠ“ iY â»Â¹áµ UT = UY)
    (hUX : IsCompact (X := X) UX) (hUX' : IsQuasiSeparated (Î± := X) UX) :
    IsIso (pushout.desc (g.appLE UX UY (by simp [â† hUY])) (iY.appLE UT UY (by simp [â† hUY]))
      (by simp only [Scheme.Hom.appLE_comp_appLE, H.w]) :
      pushout (iX.appLE US UX hUSX) (f.appLE US UT hUST) âŸ¶ Î“(Y, UY)) := by
  have hâ‚ : IsPullback (g âˆ£_ UX) (iY.resLE (f â»Â¹áµ US) (g â»Â¹áµ UX)
      (by rw [â† Scheme.Hom.comp_preimage, â† H.w]; exact g.preimage_mono hUSX))
      (iX.resLE _ _ hUSX) (f âˆ£_ US) := by
    refine .of_bot ?_ ?_ (isPullback_morphismRestrict f US)
    Â· simpa using (isPullback_morphismRestrict g UX).paste_vert H
    Â· simp [â† cancel_mono US.Î¹, H.w]
  have hâ‚‚ : IsPullback (Scheme.homOfLE _ (by simp [â† hUY])) (iY.resLE UT UY (by simp [â† hUY]))
      ((iY.resLE (f â»Â¹áµ US) (g â»Â¹áµ UX)
      (by rw [â† Scheme.Hom.comp_preimage, â† H.w]; exact g.preimage_mono hUSX)))
      (Scheme.homOfLE _ hUST) := by
    refine (IsOpenImmersion.isPullback _ _ _ _ (by simp) ?_).flip
    simp only [Scheme.opensRange_homOfLE,
      â† Scheme.Hom.comp_preimage, Scheme.Hom.resLE_comp_Î¹]
    rw [Scheme.Hom.comp_preimage, â† (g â»Â¹áµ UX).Î¹.image_injective.eq_iff]
    simp only [Scheme.Hom.image_preimage_eq_opensRange_inf, Scheme.Opens.opensRange_Î¹]
    simp [â† hUY]
  have hâ‚ƒ : IsPullback (f.resLE US UT hUST) hUT.isoSpec.hom hUS.isoSpec.hom
    (Spec.map (f.appLE _ _ hUST)) := .of_vert_isIso âŸ¨by simp [IsAffineOpen.isoSpec_hom]âŸ©
  have H := (hâ‚‚.paste_horiz hâ‚).paste_vert hâ‚ƒ
  simp only [â† Scheme.Hom.resLE_eq_morphismRestrict, Scheme.Hom.map_resLE] at H
  have inst : CompactSpace UX := isCompact_iff_compactSpace.mp hUX
  have inst : QuasiSeparatedSpace UX := (isQuasiSeparated_iff_quasiSeparatedSpace _ UX.2).mp hUX'
  have := isIso_pushoutSectionToSection_of_isQuasiSeparated _
    (f.flat_appLE hUS hUT hUST) _ _ _ H âŠ¤ isCompact_univ
    isQuasiSeparated_univ
  have : IsIso (pushout.map (iX.appLE US UX hUSX) (f.appLE US UT hUST) _ _
    (X.presheaf.map (eqToHom (show UX.Î¹ ''áµ âŠ¤ = UX by simp)).op) (ğŸ™ _) (ğŸ™ _)
      (by simp [IsAffineOpen.isoSpec_hom_appTop, Scheme.Hom.resLE_appLE]) (by simp) â‰«
      pushoutSectionToSection (f.appLE US UT hUST)
      (iX.resLE US UX hUSX â‰« hUS.isoSpec.hom)
      (iY.resLE UT UY (by simp [â† hUY]) â‰« hUT.isoSpec.hom)
      (g.resLE UX UY (by simp [â† hUY])) H âŠ¤ â‰«
      Y.presheaf.map (eqToHom (show UY = UY.Î¹ ''áµ âŠ¤ by simp)).op) :=
    inferInstance
  convert this
  ext1
  Â· simp [Scheme.Hom.app_eq_appLE, Scheme.Hom.resLE_appLE]
  Â· simp [IsAffineOpen.isoSpec_hom_appTop, Scheme.Hom.resLE_appLE]

end AlgebraicGeometry
