import Mathlib
import Mathlib.CFT.EtaleLocalization
import Mathlib.AlgebraicGeometry.Morphisms.QuasiFinite
import Mathlib.CFT.Normalization

open CategoryTheory Limits

namespace AlgebraicGeometry

universe u

variable {X Y S : Scheme.{u}} (f : X ‚ü∂ Y)

@[simps]
def _root_.TopologicalSpace.Opens.frameHom {X : Type*} [TopologicalSpace X] :
    FrameHom (TopologicalSpace.Opens X) (Set X) where
  toFun := (¬∑)
  map_inf' _ _ := rfl
  map_top' := rfl
  map_sSup' _ := by simp

lemma Scheme.Hom.exists_mem_and_isIso_morphismRestrict_toNormalization
    [LocallyOfFiniteType f] [IsSeparated f] [QuasiCompact f]
    (x : X) (hx : x ‚àà f.quasiFiniteLocus) :
    ‚àÉ V, f.toNormalization x ‚àà V ‚àß IsIso (f.toNormalization ‚à£_ V) := by
  obtain ‚ü®T, fT, u, _, hu, V, W, v, hVW, _, hv‚ÇÇ‚ü© := stacks02LN_easy _ rfl hx
  obtain ‚ü®U, hU, _‚ü© : ‚àÉ U, (pullback.snd f fT).toNormalization v.1 ‚àà U ‚àß
      IsIso ((pullback.snd f fT).toNormalization ‚à£_ U) := by
    have hVW' : (W : Set ‚Üë(pullback f fT)) = (‚ÜëV)·∂ú :=
      eq_compl_iff_isCompl.mpr (hVW.map TopologicalSpace.Opens.frameHom).symm
    have : IsClosedImmersion V.Œπ := .of_isPreimmersion _ (by simp [eq_compl_comm.mp hVW', W.isOpen])
    have : IsClosedImmersion W.Œπ := .of_isPreimmersion _ (by simpa [hVW'] using V.2)
    obtain ‚ü®H‚ü© := nonempty_isColimit_binaryCofanMk_of_isCompl V.Œπ W.Œπ (by simpa)
    let e : (pullback.snd f fT).normalization ‚âÖ V ‚®ø (W.Œπ ‚â´ pullback.snd f fT).normalization :=
      (Scheme.Hom.normalizationCoprodIso (pullback.snd f fT) H).symm ‚â™‚â´
        coprod.mapIso (asIso (V.Œπ ‚â´ pullback.snd f fT).toNormalization).symm (.refl _)
    let Œπ : V.toScheme ‚ü∂ V ‚®ø (W.Œπ ‚â´ pullback.snd f fT).normalization := coprod.inl
    refine ‚ü®e.hom ‚Åª¬π·µÅ Œπ.opensRange, ‚ü®v, ?_‚ü©, ?_‚ü©
    ¬∑ rw [‚Üê V.Œπ_apply, ‚Üê Scheme.Hom.comp_apply, ‚Üê Scheme.Hom.comp_apply]
      congr 5
      rw [‚Üê Category.assoc, ‚Üê Iso.comp_inv_eq]
      simp [Œπ, e, Scheme.Hom.normalizationCoprodIso]
    rw [‚Üê isIso_comp_right_iff _ (e.hom ‚à£_ Œπ.opensRange),
      ‚Üê morphismRestrict_comp, ‚Üê isIso_comp_right_iff _ (IsOpenImmersion.opensRangeIso Œπ).inv]
    have Heq : (pullback.snd f fT).toNormalization ‚Åª¬π·µÅ e.hom ‚Åª¬π·µÅ Scheme.Hom.opensRange Œπ = V := by
      apply le_antisymm
      ¬∑ rintro a ‚ü®b, hab‚ü©
        by_contra h
        lift a to W using hVW'.ge h
        replace hab : Œπ b = ((W.Œπ ‚â´ pullback.snd f fT).toNormalization ‚â´ coprod.inr) a := by
          have : W.Œπ ‚â´ (H.coconePointUniqueUpToIso (colimit.isColimit _)).hom = coprod.inr :=
            H.comp_coconePointUniqueUpToIso_hom _ ‚ü®.right‚ü©
          simp only [‚Üê W.Œπ_apply, ‚Üê Scheme.Hom.comp_apply, Category.assoc, e] at hab
          simpa [-Scheme.Hom.comp_base, Scheme.Hom.normalizationCoprodIso,
            reassoc_of% this] using hab
        exact Set.disjoint_iff_forall_ne.mp
          (isCompl_range_inl_inr V (W.Œπ ‚â´ pullback.snd f fT).normalization).1 ‚ü®_, rfl‚ü© ‚ü®_, rfl‚ü© hab
      ¬∑ rw [‚Üê Scheme.Hom.inv_image, ‚Üê SetLike.coe_subset_coe]
        simpa [‚Üê Scheme.Hom.opensRange_comp, Œπ, e, Scheme.Hom.normalizationCoprodIso,
          Set.range_comp] using Set.subset_preimage_image _ _
    convert (inferInstanceAs (IsIso (Scheme.isoOfEq _ Heq).hom))
    rw [Iso.comp_inv_eq, ‚Üê Iso.inv_comp_eq, ‚Üê cancel_mono (Scheme.Opens.Œπ _)]
    have : V.Œπ ‚â´ (H.coconePointUniqueUpToIso (colimit.isColimit _)).hom = coprod.inl :=
      H.comp_coconePointUniqueUpToIso_hom _ ‚ü®.left‚ü©
    simp [e, IsOpenImmersion.opensRangeIso, Scheme.Hom.normalizationCoprodIso, reassoc_of% this, Œπ]
  let fTn : (pullback.snd f fT).normalization ‚ü∂ f.normalization :=
    f.normalizationPullback fT ‚â´ pullback.fst _ _
  let U' : f.normalization.Opens := ‚ü®_, fTn.isOpenMap _ U.2‚ü©
  refine ‚ü®U', ‚ü®_, hU, by simp only [‚Üê hv‚ÇÇ, ‚Üê Scheme.Hom.comp_apply]; simp [fTn]‚ü©, ?_‚ü©
  let fTnU : U.toScheme ‚ü∂ U' := fTn.resLE _ _ (Set.subset_preimage_image _ _)
  have : Surjective fTnU := ‚ü®fun ‚ü®x, a, ha, e‚ü© ‚Ü¶ ‚ü®‚ü®a, ha‚ü©, Subtype.ext <| by simpa [fTnU] using e‚ü©‚ü©
  have H : (pullback.snd f fT).toNormalization ‚Åª¬π·µÅ U ‚â§
      pullback.fst f fT ‚Åª¬π·µÅ f.toNormalization ‚Åª¬π·µÅ U' := by
    refine fun x hx ‚Ü¶ ‚ü®_, hx, ?_‚ü©
    simp only [‚Üê Scheme.Hom.comp_apply]
    congr 5
    simp [fTn]
  have : IsPullback ((pullback.snd f fT).toNormalization ‚à£_ U)
      ((pullback.fst f fT).resLE _ _ H) fTnU (f.toNormalization ‚à£_ U') := by
    refine .of_bot (t := isPullback_morphismRestrict ..) ?_ ?_
    ¬∑ simp only [Scheme.Hom.resLE_comp_Œπ, fTnU]
      refine .paste_vert (isPullback_morphismRestrict ..) ?_
      have H : IsPullback (pullback.map _ _ _ _ f.toNormalization (ùüô _) (ùüô _) (by simp) (by simp))
          (pullback.fst f fT) (pullback.fst f.fromNormalization fT) f.toNormalization :=
        .of_right (t := .flip <| .of_hasPullback ..)
          (by simpa using (.flip <| .of_hasPullback ..)) (by cat_disch)
      exact .of_iso' H (.refl _) (asIso <| f.normalizationPullback fT) (.refl _) (.refl _)
        (by cat_disch) (by simp) (by simp [fTn]) (by simp)
    ¬∑ simp [‚Üê cancel_mono U'.Œπ, fTnU, fTn]
  refine MorphismProperty.of_isPullback_of_descendsAlong (P := .isomorphisms _)
    (Q := @Surjective ‚äì @Flat ‚äì @LocallyOfFinitePresentation) this
    ‚ü®‚ü®‚Äπ_‚Ä∫, inferInstance‚ü©, inferInstance‚ü© ‚Äπ_‚Ä∫

/--
**Zariski's main theorem**

Recall that any qcqs morphism `f : X ‚ü∂ Y` factors through the relative normalization via
`f.toNormalization : X ‚ü∂ f.normalization` (a dominant morphism) and
`f.fromNormalization : f.normalization ‚ü∂ Y` (an integral morphism).

Let `f : X ‚ü∂ Y` be separated and of finite type.

then there exists `U : f.normalization.Opens`, such that
1. `f.toNormalization ‚à£_ U` is an isomorphism
2. `f.toNormalization ‚Åª¬π·µÅ U` is the quasi-finite locus of `f`
-/
@[stacks 03GW]
lemma Scheme.Hom.exists_isIso_morphismRestrict_toNormalization
    [LocallyOfFiniteType f] [IsSeparated f] [QuasiCompact f] :
    ‚àÉ U : f.normalization.Opens, IsIso (f.toNormalization ‚à£_ U) ‚àß
      (f.toNormalization ‚Åª¬π·µÅ U).1 = f.quasiFiniteLocus := by
  choose V hxV hV using fun x : f.quasiFiniteLocus ‚Ü¶
    f.exists_mem_and_isIso_morphismRestrict_toNormalization x x.2
  let ùí∞ := Opens.iSupOpenCover V
  have : IsIso (f.toNormalization ‚à£_ ‚®Ü x, V x) := by
    refine (IsZariskiLocalAtTarget.iff_of_openCover (P := .isomorphisms _) ùí∞).mpr fun x ‚Ü¶ ?_
    refine (MorphismProperty.arrow_mk_iso_iff (.isomorphisms _)
      ((morphismRestrictRestrict ..).symm ‚â™‚â´ morphismRestrictOpensRange ..)).mp ?_
    have : Opens.Œπ _ ''·µÅ (ùí∞.f x).opensRange = V x := by
      simp only [Opens.iSupOpenCover, ùí∞, ‚Üê opensRange_comp, homOfLE_Œπ, Opens.opensRange_Œπ]
    convert hV x
  refine ‚ü®‚®Ü x : f.quasiFiniteLocus, V x, this, ?_‚ü©
  ext x
  suffices (‚àÉ i : quasiFiniteLocus f, toNormalization f x ‚àà V i) ‚Üî x ‚àà quasiFiniteLocus f by
    simpa
  refine ‚ü®?_, fun h ‚Ü¶ ‚ü®‚ü®x, h‚ü©, hxV _‚ü©‚ü©
  rintro ‚ü®y, hxVy‚ü©
  obtain ‚ü®U, r, hU, hr, hxV, hrV‚ü© : ‚àÉ (U : Y.Opens) (r : Œì(_, f.fromNormalization ‚Åª¬π·µÅ U)),
      IsAffineOpen U ‚àß IsAffineOpen (f.toNormalization ‚Åª¬π·µÅ f.normalization.basicOpen r) ‚àß
      x ‚àà f.toNormalization ‚Åª¬π·µÅ f.normalization.basicOpen r ‚àß Scheme.basicOpen _ r ‚â§ V y := by
    obtain ‚ü®_, ‚ü®W, hW, rfl‚ü©, hxW, hWV : W ‚â§ _‚ü© := X.isBasis_affineOpens.exists_subset_of_mem_open
      hxVy (f.toNormalization ‚Åª¬π·µÅ V y).isOpen
    have : IsAffine W := hW
    let V' := (X.homOfLE hWV ‚â´ f.toNormalization ‚à£_ V y ‚â´ (V y).Œπ).opensRange
    have hV' : IsAffineOpen V' := isAffineOpen_opensRange _
    have hV'V : V' ‚â§ V y := by
      simp_rw [V', ‚Üê Category.assoc, opensRange_comp]
      exact (image_le_opensRange _ _).trans (by simp)
    have hV'W : f.toNormalization ‚Åª¬π·µÅ V' = W := by
      have : (f.toNormalization ‚Åª¬π·µÅ V y).Œπ ‚Åª¬π·µÅ f.toNormalization ‚Åª¬π·µÅ V' =
          (f.toNormalization ‚Åª¬π·µÅ V y).Œπ ‚Åª¬π·µÅ W := by
        rw [‚Üê Scheme.Hom.comp_preimage, ‚Üê morphismRestrict_Œπ]
        simp only [V', opensRange_comp, Scheme.Hom.preimage_image_eq, opensRange_homOfLE]
      simpa only [image_preimage_eq_opensRange_inf, Opens.opensRange_Œπ, ‚Üê preimage_inf,
        inf_eq_right.mpr, hV'V, hWV] using congr((f.toNormalization ‚Åª¬π·µÅ V y).Œπ ''·µÅ $this)
    obtain ‚ü®_, ‚ü®U, hU, rfl‚ü©, hxU, -‚ü© := Y.isBasis_affineOpens.exists_subset_of_mem_open
      (Set.mem_univ (f x)) isOpen_univ
    obtain ‚ü®f‚ÇÅ, f‚ÇÇ, e, hxf‚ü© := exists_basicOpen_le_affine_inter (hU.preimage f.fromNormalization)
      hV' (f.toNormalization x) ‚ü®by simpa [‚Üê Scheme.Hom.comp_apply], hV'W.ge hxW‚ü©
    refine ‚ü®U, f‚ÇÅ, hU, ?_, hxf, (e.trans_le (f.normalization.basicOpen_le _)).trans hV'V‚ü©
    rw [e, preimage_basicOpen]
    exact IsAffineOpen.basicOpen (hV'W ‚ñ∏ hW) _
  let W := f.toNormalization ‚Åª¬π·µÅ f.normalization.basicOpen r
  have H : W ‚â§ f ‚Åª¬π·µÅ U := by
    unfold W
    grw [Scheme.basicOpen_le, ‚Üê Scheme.Hom.comp_preimage, f.toNormalization_fromNormalization]
  have H' : f.fromNormalization.appLE _ _ ((normalization f).basicOpen_le _) ‚â´
    f.toNormalization.app _ = f.appLE U W H := by
    simp only [app_eq_appLE]
    exact (appLE_comp_appLE _ _ _ _ _ _ _).trans (by simp [W])
  have : IsIso ((toNormalization f).app ((normalization f).basicOpen r)) := by
    have H : (f.toNormalization ‚à£_ V y) ‚Åª¬π·µÅ (V y).Œπ ‚Åª¬π·µÅ (normalization f).basicOpen r =
        (Scheme.homOfLE _ (f.toNormalization.preimage_mono hrV)).opensRange := by
      apply Scheme.Hom.image_injective (f.toNormalization ‚à£_ V y)
      simp only [opensRange_homOfLE, image_preimage_eq_opensRange_inf]
      rw [‚Üê Scheme.Hom.comp_preimage, ‚Üê morphismRestrict_Œπ, Scheme.Hom.comp_preimage,
        image_preimage_eq_opensRange_inf]
    have := (inferInstanceAs (IsIso ((toNormalization f ‚à£_ V y).app
      (Scheme.homOfLE _ hrV).opensRange)))
    simp only [Opens.toScheme_presheaf_obj, app_eq_appLE, morphismRestrict_appLE] at this ‚ä¢
    convert this <;>
      simp [Scheme.Hom.image_preimage_eq_opensRange_inf, -Scheme.preimage_basicOpen,
        f.toNormalization.preimage_mono, hrV, H]
  have : (f.appLE U W H).hom.QuasiFinite := by
    have : (f.appLE U W H).hom.FiniteType :=
      LocallyOfFiniteType.finiteType_of_affine_subset ‚ü®_, hU‚ü© ‚ü®_, hr‚ü© _
    rw [‚Üê H', CommRingCat.hom_comp, RingHom.finiteType_respectsIso.cancel_right_isIso] at this
    rw [‚Üê H', CommRingCat.hom_comp, RingHom.QuasiFinite.respectsIso.cancel_right_isIso]
    exact .of_isIntegral_of_finiteType (IsIntegralHom.isIntegral_app f.fromNormalization _ hU)
      ‚ü®r, (hU.preimage f.fromNormalization).isLocalization_basicOpen _‚ü© this
  have hxU : f x ‚àà U := by
    convert show _ ‚àà U from (normalization f).basicOpen_le _ hxV
    rw [‚Üê Scheme.Hom.comp_apply, f.toNormalization_fromNormalization]
  refine .of_comp (g := (Y.presheaf.germ U _ hxU).hom) ?_
  rw [‚Üê CommRingCat.hom_comp, f.germ_stalkMap, ‚Üê X.presheaf.germ_res (homOfLE H) _ hxV,
    app_eq_appLE, appLE_map_assoc, CommRingCat.hom_comp]
  refine .comp ?_ this
  have := hr.isLocalization_stalk ‚ü®x, hxV‚ü©
  let := X.presheaf.algebra_section_stalk ‚ü®x, hxV‚ü©
  rw [‚Üê RingHom.algebraMap_toAlgebra (X.presheaf.germ _ _ _).hom, @RingHom.quasiFinite_algebraMap]
  exact .of_isLocalization (hr.primeIdealOf ‚ü®x, hxV‚ü©).asIdeal.primeCompl

lemma Scheme.Hom.isOpen_quasiFiniteLocus
    [LocallyOfFiniteType f] [IsSeparated f] [QuasiCompact f] :
    IsOpen f.quasiFiniteLocus := by
  obtain ‚ü®U, hU, e‚ü© := Scheme.Hom.exists_isIso_morphismRestrict_toNormalization f
  exact e ‚ñ∏ (f.toNormalization ‚Åª¬π·µÅ U).2

def Scheme.Hom.quasiFiniteOpen
    [LocallyOfFiniteType f] [IsSeparated f] [QuasiCompact f] : X.Opens :=
  ‚ü®_, f.isOpen_quasiFiniteLocus‚ü©

instance [LocallyOfFiniteType f] [IsSeparated f] [QuasiCompact f] :
    IsOpenImmersion (f.quasiFiniteOpen.Œπ ‚â´ f.toNormalization) := by
  obtain ‚ü®U, hU, e‚ü© := Scheme.Hom.exists_isIso_morphismRestrict_toNormalization f
  convert inferInstanceAs (IsOpenImmersion ((X.isoOfEq (U := f.quasiFiniteOpen)
    (SetLike.coe_injective e.symm)).hom ‚â´ f.toNormalization ‚à£_ U ‚â´ U.Œπ)) using 1
  simp

lemma Scheme.Hom.quasiFiniteOpen_eq_top [LocallyQuasiFinite f]
    [LocallyOfFiniteType f] [IsSeparated f] [QuasiCompact f] : f.quasiFiniteOpen = ‚ä§ :=
  TopologicalSpace.Opens.ext f.quasiFiniteLocus_eq_univ

instance [LocallyQuasiFinite f] [LocallyOfFiniteType f] [IsSeparated f] [QuasiCompact f] :
    IsOpenImmersion f.toNormalization := by
  convert inferInstanceAs (IsOpenImmersion (X.topIso.inv ‚â´ (X.isoOfEq
    f.quasiFiniteOpen_eq_top).inv ‚â´ f.quasiFiniteOpen.Œπ ‚â´ f.toNormalization)) using 1
  simp

-- In particular it is surjective (by infer_instance), since it is a priori dominant.
instance [QuasiSeparated f] [UniversallyClosed f] : UniversallyClosed f.toNormalization :=
  have : UniversallyClosed (f.toNormalization ‚â´ f.fromNormalization) := by simpa
  .of_comp_of_isSeparated _ f.fromNormalization

lemma IsFinite.of_isProper_of_locallyQuasiFinite
    [IsProper f] [LocallyQuasiFinite f] : IsFinite f := by
  have : IsIso f.toNormalization :=
    (isIso_iff_isOpenImmersion_and_surjective _).mpr ‚ü®inferInstance, inferInstance‚ü©
  refine (IsFinite.iff_isIntegralHom_and_locallyOfFiniteType _).mpr ‚ü®?_, inferInstance‚ü©
  rw [‚Üê f.toNormalization_fromNormalization]
  infer_instance

lemma IsFinite.iff_isProper_and_locallyQuasiFinite :
    IsFinite f ‚Üî IsProper f ‚àß LocallyQuasiFinite f := by
  refine ‚ü®fun _ ‚Ü¶ ‚ü®inferInstance, inferInstance‚ü©,
    fun ‚ü®_, _‚ü© ‚Ü¶ .of_isProper_of_locallyQuasiFinite f‚ü©

end AlgebraicGeometry
