/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.Algebra.Polynomial.Eval.Irreducible
import Mathlib.RingTheory.Adjoin.PowerBasis
import Mathlib.RingTheory.DedekindDomain.Dvr
import Mathlib.RingTheory.Smooth.Local
import Mathlib.RingTheory.Smooth.StandardSmoothCotangent
import Mathlib.RingTheory.Unramified.LocalRing

/-!

# Existence of local algebra with given residue field.

Let `(R, m, k)` be a local ring, `p : R[X]` be a monic polynomial
that is irreducible and separable in `k[X]` (See `SeparableResidueStruct`).
We show that `R[X]/p` is a finite etale local `R`-algebra with residue field `k[X]/p`.
Furthermore, it is a domain when `R` is a UFD, and is a DVR when `R` also is.

In particular if `p` is the minimal polynomial of a primitive element of some finite separable
extension `K` of `k`, then `R[X]/p` is a finite etale `R`-algebra with residue field `K`.
(See `SeparableResidueStruct.exists_of_isSeparable`)

-/

open IsLocalRing Polynomial TensorProduct KaehlerDifferential IntermediateField

variable {R : Type*} [CommRing R] [IsLocalRing R]

local notation "ğ“€[" R "]" => ResidueField R
local notation "ğ“‚[" R "]" => maximalIdeal R

variable (R) in
/-- The structure of a monic polynomial over a local ring, whose image is irreducible
and separable in the residue field.

This is a separate structure because we would like to put instances on `AdjoinRoot p` depending
on these hypotheses. -/
structure SeparableResidueStruct : Type _ where
  /-- The polynomial of a `SeparableResidueStruct`. -/
  p : R[X]
  monic : p.Monic
  irreducible_map_residue : Irreducible (p.map (residue R))
  separable_map_residue : (p.map (residue R)).Separable

noncomputable section

namespace SeparableResidueStruct

variable (ğ“Ÿ : SeparableResidueStruct R)

/-- The ring extension `R[X]/p` corresponding to a `SeparableResidueStruct`.

This is a local ring, is finite etale over `R` and is a domain when `R` is a UFD. -/
protected abbrev Ring : Type _ := AdjoinRoot ğ“Ÿ.p

instance : Algebra R[X] ğ“Ÿ.Ring := inferInstanceAs (Algebra R[X] (R[X] â§¸ _))

instance : IsScalarTower R R[X] ğ“Ÿ.Ring := inferInstanceAs (IsScalarTower R R[X] (R[X] â§¸ _))

lemma algebraMap_polynomial : algebraMap R[X] ğ“Ÿ.Ring = AdjoinRoot.mk _ := rfl

instance : Module.Free R ğ“Ÿ.Ring := ğ“Ÿ.monic.free_adjoinRoot
instance : Module.Finite R ğ“Ÿ.Ring := ğ“Ÿ.monic.finite_adjoinRoot

/-- The map from `R[X]/p` to `Îº[x]/p`. -/
def toResidueExt : ğ“Ÿ.Ring â†’â‚[R] AdjoinRoot (ğ“Ÿ.p.map (residue R)) :=
  AdjoinRoot.liftAlgHom _ (Algebra.ofId _ _) (.root _)
    (by simp [â† aeval_def, â† aeval_map_algebraMap (A := ğ“€[R])]; simp)

@[simp]
lemma toResidueExt_mk (q) : ğ“Ÿ.toResidueExt (.mk _ q) = .mk _ (q.map (residue R)) := by
  change ğ“Ÿ.toResidueExt.comp (AdjoinRoot.mkâ‚ _) q =
      (((AdjoinRoot.mkâ‚ _).restrictScalars R).comp (Polynomial.mapAlg _ _)) q
  congr 1; ext; simp [toResidueExt, mapAlg]

@[simp]
lemma toResidueExt_root : ğ“Ÿ.toResidueExt (.root _) = .root _ := by
  rw [AdjoinRoot.root, toResidueExt_mk, AdjoinRoot.root, map_X]

lemma toResidueExt_eq : ğ“Ÿ.toResidueExt =
    (AdjoinRoot.quotEquivQuotMap _ _).toAlgHom.comp (Ideal.Quotient.mkâ‚ _ _) := by
  ext
  change _ = Ideal.Quotient.mk _ (X.map _)
  simp
  rfl

instance : Field (AdjoinRoot (ğ“Ÿ.p.map (residue R))) :=
  haveI : Fact (Irreducible (ğ“Ÿ.p.map (residue R))) := âŸ¨ğ“Ÿ.irreducible_map_residueâŸ©
  inferInstance

instance : Algebra.IsSeparable ğ“€[R] (AdjoinRoot (ğ“Ÿ.p.map (residue R))) := by
  let pb := AdjoinRoot.powerBasis (ğ“Ÿ.monic.map (residue R)).ne_zero
  suffices Algebra.IsSeparable ğ“€[R] ğ“€[R]âŸ®pb.genâŸ¯ from
    .of_algHom _ _ pb.equivAdjoinSimple.symm.toAlgHom
  rw [isSeparable_adjoin_simple_iff_isSeparable, IsSeparable,
    AdjoinRoot.minpoly_powerBasis_gen_of_monic (ğ“Ÿ.monic.map (residue R))]
  exact ğ“Ÿ.separable_map_residue

lemma toResidueExt_surjective : Function.Surjective ğ“Ÿ.toResidueExt :=
  ğ“Ÿ.toResidueExt_eq â–¸ (AdjoinRoot.quotEquivQuotMap _ _).surjective.comp Ideal.Quotient.mk_surjective

lemma ker_toResidueExt : RingHom.ker ğ“Ÿ.toResidueExt.toRingHom = ğ“‚[R].map (algebraMap _ _) := by
  rw [ğ“Ÿ.toResidueExt_eq, AlgHom.toRingHom_eq_coe, AlgHom.comp_toRingHom,
    RingHom.ker_comp_of_injective _ (AdjoinRoot.quotEquivQuotMap _ _).injective,
    Ideal.Quotient.mkâ‚_ker, AdjoinRoot.algebraMap_eq]

instance isMaximal_map : (ğ“‚[R].map (algebraMap R ğ“Ÿ.Ring)).IsMaximal :=
  ğ“Ÿ.ker_toResidueExt â–¸ RingHom.ker_isMaximal_of_surjective _ ğ“Ÿ.toResidueExt_surjective

instance : IsLocalRing ğ“Ÿ.Ring := .of_unique_max_ideal âŸ¨_, ğ“Ÿ.isMaximal_map, fun J hJ â†¦
    have := eq_maximalIdeal (Ideal.isMaximal_comap_of_isIntegral_of_isMaximal (R := R) J)
    (ğ“Ÿ.isMaximal_map.eq_of_le hJ.ne_top (Ideal.map_le_iff_le_comap.mpr this.ge)).symmâŸ©

lemma maximalIdeal_ring : ğ“‚[ğ“Ÿ.Ring] = ğ“‚[R].map (algebraMap R ğ“Ÿ.Ring) :=
  (eq_maximalIdeal inferInstance).symm

lemma mk_mem_maximalIdeal_iff {q} :
    .mk ğ“Ÿ.p q âˆˆ ğ“‚[ğ“Ÿ.Ring] â†” ğ“Ÿ.p.map (residue R) âˆ£ q.map (residue R) := by
  rw [maximalIdeal_ring, â† ker_toResidueExt, RingHom.mem_ker, AlgHom.toRingHom_eq_coe,
    RingHom.coe_coe, toResidueExt_mk, AdjoinRoot.mk_eq_zero]

/-- The residue field of `R[X]/p` is isomorphic to `Îº[x]/p`. -/
def residueFieldEquiv : ğ“€[ğ“Ÿ.Ring] â‰ƒâ‚[ğ“€[R]] AdjoinRoot (ğ“Ÿ.p.map (residue R)) where
  __ := (Ideal.quotEquivOfEq (ğ“Ÿ.maximalIdeal_ring.trans ğ“Ÿ.ker_toResidueExt.symm)).trans
    (Ideal.quotientKerAlgEquivOfSurjective ğ“Ÿ.toResidueExt_surjective).toRingEquiv
  commutes' := residue_surjective.forall.mpr fun x â†¦ by
    change ğ“Ÿ.toResidueExt (.mk _ (C x)) = _
    simp [toResidueExt]
    rfl

instance : Algebra.FormallyUnramified R ğ“Ÿ.Ring := by
  rw [Algebra.FormallyUnramified.iff_map_maximalIdeal_eq]
  exact âŸ¨.of_algHom _ _ ğ“Ÿ.residueFieldEquiv.toAlgHom, ğ“Ÿ.maximalIdeal_ring.symmâŸ©

instance : Algebra.FormallySmooth R ğ“Ÿ.Ring := by
  let S := ğ“Ÿ.Ring
  let P : Algebra.Extension R S :=
  { Ring := R[X]
    Ïƒ := Function.surjInv Ideal.Quotient.mk_surjective
    algebraMap_Ïƒ := Function.surjInv_eq _ }
  have hPker : P.ker = .span {ğ“Ÿ.p} := Ideal.mk_ker
  have hP : P.ker.FG := âŸ¨{ğ“Ÿ.p}, Finset.coe_singleton _ â–¸ hPker.symmâŸ©
  let eP : ğ“€[S] âŠ—[S] P.CotangentSpace â‰ƒâ‚—[S] ğ“€[S] :=
    (TensorProduct.AlgebraTensorModule.congr (.refl _ _)
      ((TensorProduct.AlgebraTensorModule.congr (.refl _ _)
      (KaehlerDifferential.polynomialEquiv R)) â‰ªâ‰«â‚—
      TensorProduct.AlgebraTensorModule.rid _ _ _)) â‰ªâ‰«â‚—
    TensorProduct.AlgebraTensorModule.rid _ _ _
  have : Module.Free P.Ring Î©[P.Ringâ„R] := .of_equiv (KaehlerDifferential.polynomialEquiv R).symm
  rw [Algebra.FormallySmooth.iff_injective_lTensor_residueField P hP, injective_iff_map_eq_zero]
  intro a ha
  obtain âŸ¨a, rflâŸ© := TensorProduct.mk_surjective S _ _ residue_surjective a
  obtain âŸ¨âŸ¨a, ha'âŸ©, rflâŸ© := Algebra.Extension.Cotangent.mk_surjective a
  replace ha : (polynomialEquiv R (D _ _ a) â€¢ 1 : S) â€¢ 1 = eP 0 := DFunLike.congr_arg eP ha
  obtain âŸ¨q, rflâŸ© : ğ“Ÿ.p âˆ£ a := by rwa [hPker, Ideal.mem_span_singleton] at ha'
  rw [eP.map_zero, polynomialEquiv_D, smul_assoc, one_smul, â† Algebra.algebraMap_eq_smul_one,
    IsScalarTower.algebraMap_apply R[X] S ğ“€[S], ResidueField.algebraMap_eq,
    residue_eq_zero_iff, algebraMap_polynomial, derivative_mul, map_add, add_comm, map_mul,
    AdjoinRoot.mk_self, zero_mul, zero_add, map_mul] at ha
  have H : AdjoinRoot.mk ğ“Ÿ.p (derivative ğ“Ÿ.p) âˆ‰ ğ“‚[S] := ğ“Ÿ.mk_mem_maximalIdeal_iff.not.mpr fun h â†¦
    ğ“Ÿ.irreducible_map_residue.not_isUnit
      (ğ“Ÿ.separable_map_residue.isUnit_of_dvd (derivative_map ğ“Ÿ.p (residue R) â–¸ h))
  have : Algebra.Extension.Cotangent.mk (P := P) âŸ¨ğ“Ÿ.p * q, ha'âŸ© =
      AdjoinRoot.mk ğ“Ÿ.p q â€¢ .mk âŸ¨ğ“Ÿ.p, hPker â–¸ Ideal.mem_span_singleton_self _âŸ© := by
    change Algebra.Extension.Cotangent.mk (P := P) âŸ¨ğ“Ÿ.p * q, ha'âŸ© = (show P.Ring from q) â€¢ .mk _
    ext
    rw [Algebra.Extension.Cotangent.val_smul']
    simp only [Algebra.Extension.Cotangent.val_mk, â† map_smul, SetLike.mk_smul_mk, smul_eq_mul]
    congr 2
    rw [mul_comm]
  dsimp
  rw [this, â† TensorProduct.smul_tmul, â† Algebra.algebraMap_eq_smul_one,
    ResidueField.algebraMap_eq,
    (residue_eq_zero_iff _).mpr ((Ideal.IsPrime.mem_or_mem' ha).resolve_left H), zero_tmul]

instance : Algebra.FormallyEtale R ğ“Ÿ.Ring := .of_formallyUnramified_and_formallySmooth

instance : Algebra.Etale R ğ“Ÿ.Ring where

lemma irreducible [IsDomain R] : Irreducible ğ“Ÿ.p :=
  ğ“Ÿ.monic.irreducible_of_irreducible_map _ _ ğ“Ÿ.irreducible_map_residue

instance [IsDomain R] [UniqueFactorizationMonoid R] : IsDomain ğ“Ÿ.Ring :=
  AdjoinRoot.isDomain_of_prime (irreducible_iff_prime.mp ğ“Ÿ.irreducible)

instance [IsNoetherianRing R] : IsNoetherianRing ğ“Ÿ.Ring := .of_finite R _

instance {R : Type*} [CommRing R] [IsDomain R] [IsDiscreteValuationRing R]
    (ğ“Ÿ : SeparableResidueStruct R) : IsDiscreteValuationRing ğ“Ÿ.Ring := by
  have : Â¬ IsField ğ“Ÿ.Ring := mt (isField_of_isIntegral_of_isField
    (FaithfulSMul.algebraMap_injective R ğ“Ÿ.Ring)) (IsDiscreteValuationRing.not_isField R)
  refine ((IsDiscreteValuationRing.TFAE _ this).out 0 4).mpr ?_
  rw [maximalIdeal_ring]
  infer_instance

/-- If `R` is a local ring with residue field `Îº`, and `K` is a finite separable extension of `Îº`,
then there exists a finite etale local `R`-algebra whose residue field is `K`. -/
lemma exists_of_isSeparable {K : Type*} [Field K] [Algebra ğ“€[R] K]
    [FiniteDimensional ğ“€[R] K] [Algebra.IsSeparable ğ“€[R] K] :
    âˆƒ (ğ“Ÿ : SeparableResidueStruct R), Nonempty (ğ“€[ğ“Ÿ.Ring] â‰ƒâ‚[ğ“€[R]] K) := by
  letI := Algebra.compHom K (residue R)
  have : IsScalarTower R ğ“€[R] K := .of_algebraMap_eq' rfl
  obtain âŸ¨x, hxâŸ© := Field.exists_primitive_element ğ“€[R] K
  have hx' := Algebra.IsIntegral.isIntegral (R := ğ“€[R]) x
  obtain âŸ¨p, hp, hpdeg, hpmonâŸ© := lifts_and_degree_eq_and_monic ((Set.range_eq_univ.mpr
    (Polynomial.map_surjective _ residue_surjective)).ge (Set.mem_univ (minpoly ğ“€[R] x)))
    (minpoly.monic hx')
  have (eq := hğ“Ÿ) ğ“Ÿ : SeparableResidueStruct R :=
    âŸ¨p, hpmon, hp â–¸ minpoly.irreducible hx', hp â–¸ Algebra.IsSeparable.isSeparable ğ“€[R] xâŸ©
  refine âŸ¨ğ“Ÿ, âŸ¨ğ“Ÿ.residueFieldEquiv.trans ?_âŸ©âŸ©
  refine (AdjoinRoot.powerBasis ğ“Ÿ.irreducible_map_residue.ne_zero).equivOfMinpoly
    (PowerBasis.ofAdjoinEqTop hx' (Algebra.adjoin_eq_top_of_intermediateField
      (fun _ _ â†¦ Algebra.IsAlgebraic.isAlgebraic _) hx)) ?_
  rw [AdjoinRoot.minpoly_powerBasis_gen_of_monic (ğ“Ÿ.monic.map _)]
  simp [hğ“Ÿ, hp]

end SeparableResidueStruct
