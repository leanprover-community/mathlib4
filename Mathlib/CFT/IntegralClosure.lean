module

public import Mathlib.CFT.IsStandardEtale
public import Mathlib.CFT.StandardSmooth
public import Mathlib.CFT.SymmetricPolynomial
public import Mathlib.LinearAlgebra.Lagrange
public import Mathlib.RingTheory.Polynomial.Cyclotomic.Roots
public import Mathlib.RingTheory.RingHom.Etale
public import Mathlib.RingTheory.RingHom.StandardSmooth
public import Mathlib.RingTheory.Smooth.Flat

@[expose] public section

variable {R S : Type*} [CommRing R] [CommRing S] [Algebra R S]


open Polynomial

open scoped Finset nonZeroDivisors in
theorem Polynomial.eq_zero_of_degree_lt_of_eval_eq_zero {f : R[X]}
    (s : Finset R) (degree_f_lt : f.degree < â†‘(#s)) (eval_f : âˆ€ x âˆˆ s, eval x f = 0)
    (hs : (s : Set R).Pairwise (Â· - Â· âˆˆ Râ°)) : f = 0 := by
  classical
  nontriviality R
  induction s using Finset.induction generalizing f with
  | empty => simpa using degree_f_lt
  | insert a s has IH =>
    by_contra hf
    have hfs : f.degree â‰¤ #s := by
      simp only [degree_eq_natDegree hf, Nat.cast_lt, Nat.cast_le] at degree_f_lt âŠ¢
      simpa [has, Nat.lt_add_one_iff] using degree_f_lt
    have := IH (f := f /â‚˜ (X - C a)) ((degree_divByMonic_lt _ (monic_X_sub_C _) hf
      (by simp)).trans_le hfs) (fun x hx â†¦ by
      have : (x - a) * eval x (f /â‚˜ (X - C a)) = 0 := by
        simpa [eval_f a, eval_f x (Finset.mem_insert_of_mem hx)] using
          congr($(modByMonic_add_div f (monic_X_sub_C a)).eval x)
      exact (hs (Finset.mem_insert_of_mem hx) (Finset.mem_insert_self _ _) (by grind):).1 _ this)
      (Set.pairwise_insert.mp (by simpa using hs)).1
    simpa [this, eval_f a, Ne.symm hf] using modByMonic_add_div f (monic_X_sub_C a)

lemma IsPrimitiveRoot.mk_of_natPrime {p : â„•} (hp : p.Prime) {Î¶ : R}
    (hÎ¶ : Î¶ ^ p = 1) (hÎ¶' : Î¶ â‰  1) : IsPrimitiveRoot Î¶ p := by
  obtain âŸ¨k, hk, hÎ¶kâŸ© := IsPrimitiveRoot.exists_pos hÎ¶ hp.ne_zero
  by_cases hk1 : k = 1
  Â· simpa [hk1, hÎ¶'] using hÎ¶k.1
  Â· exact (hp.dvd_iff_eq hk1).mp (hÎ¶k.2 _ hÎ¶) â–¸ hÎ¶k

lemma IsPrimitiveRoot.cyclotomic_eq_of_isDomain [IsDomain R]
    {n : â„•} {Î¶ : R} (hÎ¶ : IsPrimitiveRoot Î¶ n) :
    âˆ i âˆˆ .range n with n.Coprime i, (X - C (Î¶ ^ i)) = cyclotomic n R := by
  by_cases hn : n = 0; Â· simp [hn]
  by_cases hnâ‚ : n = 1; Â· simp [hnâ‚]
  replace hnâ‚ : 1 < n := by lia
  classical
  let s := ((Finset.range n).filter (n.Coprime Â·)).image (Î¶ ^ Â·)
  have hs : s.card = n.totient := by
    rw [Finset.card_image_of_injOn (hÎ¶.injOn_pow.mono (by grind))]
    simp [Nat.totient_eq_card_coprime]
  rw [eq_comm]
  apply Polynomial.eq_of_degree_sub_lt_of_eval_finset_eq s
  Â· refine (degree_sub_lt ?_ ?_ ?_).trans_le ?_
    Â· rw [degree_prod_of_monic _ _ fun _ _ â†¦ monic_X_sub_C _]
      simp [degree_X_sub_C, -map_pow, Nat.totient_eq_card_coprime, degree_cyclotomic]
    Â· rw [ne_eq, â† degree_eq_bot]; simp [degree_cyclotomic]
    Â· rw [monic_prod_of_monic _ _ fun _ _ â†¦ monic_X_sub_C _, cyclotomic.monic]
    Â· simp [degree_cyclotomic, hs]
  Â· simp only [Finset.mem_image, Finset.mem_filter, Finset.mem_range, map_pow, forall_exists_index,
      and_imp, s]
    rintro _ i hin hni rfl
    rw [((hÎ¶.pow_iff_coprime (Ne.bot_lt hn) _).mpr hni.symm).isRoot_cyclotomic (Ne.bot_lt hn)]
    rw [eval_prod, Finset.prod_eq_zero (i := i) (by simpa [hin])]
    simp

open Polynomial in
lemma cyclotomic_eq_prod_of_eval_eq_zero {R : Type*} [CommRing R]
    {n : â„•} {Î¶ : R} (hÎ¶ : eval Î¶ (cyclotomic n R) = 0) :
    âˆ i âˆˆ .range n with n.Coprime i, (X - C (Î¶ ^ i)) = cyclotomic n R := by
  obtain rfl | hn := eq_or_ne n 0
  Â· simp
  let f : AdjoinRoot (cyclotomic n â„¤) â†’+* R := AdjoinRoot.lift (algebraMap â„¤ R) Î¶
    (by rwa [â† eval_map, map_cyclotomic])
  have : IsDomain (AdjoinRoot (cyclotomic n â„¤)) := AdjoinRoot.isDomain_of_prime
    (cyclotomic.irreducible hn.bot_lt).prime
  have : NoZeroSMulDivisors â„¤ (AdjoinRoot (cyclotomic n â„¤)) :=
    AdjoinRoot.noZeroSMulDivisors_of_prime_of_degree_ne_zero
      (cyclotomic.irreducible hn.bot_lt).prime
    (by simp [degree_cyclotomic, hn])
  have : CharZero (AdjoinRoot (cyclotomic n â„¤)) := .of_addMonoidHom
    (algebraMap â„¤ _).toAddMonoidHom (by simp) (FaithfulSMul.algebraMap_injective _ _)
  have : IsPrimitiveRoot (AdjoinRoot.root (cyclotomic n â„¤)) n :=
    have : NeZero (n : AdjoinRoot (cyclotomic n â„¤)) := âŸ¨by simpaâŸ©
    isRoot_cyclotomic_iff.mp (by
      rw [â† map_cyclotomic n (algebraMap â„¤ (AdjoinRoot (cyclotomic n â„¤))), IsRoot,
        eval_map_algebraMap]
      simp)
  have := congr($(this.cyclotomic_eq_of_isDomain).map f)
  simpa [Polynomial.map_prod, f] using this

lemma Polynomial.IsRoot.isUnit {p : R[X]} {x : R} (h : p.IsRoot x) (hp : IsUnit (p.coeff 0)) :
    IsUnit x := by
  refine isUnit_of_dvd_unit ?_ hp.neg
  exact âŸ¨_, .symm <| by simpa [h.eq_zero, add_eq_zero_iff_eq_neg', modByMonic_X,
    â† coeff_zero_eq_eval_zero] using congr($(p.modByMonic_add_div monic_X).eval x)âŸ©

theorem isUnit_pow_sub_pow_of_isRoot_cyclotomic {p : â„•} (hp : p.Prime) (hp' : IsUnit (p : R))
    {Î¶ : R} (hÎ¶ : (cyclotomic p R).IsRoot Î¶) (i j : Fin p) (hij : i â‰  j) :
    IsUnit (Î¶ ^ i.1 - Î¶ ^ j.1) := by
  wlog hij : i < j generalizing i j
  Â· simpa using (this j i (.symm â€¹_â€º) (lt_of_le_of_ne (le_of_not_gt hij) (.symm â€¹_â€º))).neg
  have := Fact.mk hp
  have : âˆ x âˆˆ Finset.range p with p.Coprime x, (1 - Î¶ ^ x) = p := by
    simpa [eval_prod, cyclotomic_prime] using
      congr($(cyclotomic_eq_prod_of_eval_eq_zero hÎ¶).eval 1)
  rw [â† Nat.sub_add_cancel (show i.1 â‰¤ j.1 from hij.le), â† one_mul (Î¶ ^ i.1), pow_add, â† sub_mul]
  refine .mul (isUnit_of_dvd_unit ?_ (hp'.map (algebraMap R _))) (.pow _ ?_)
  Â· simp only [Algebra.algebraMap_self, â† this, map_prod, RingHom.id_apply]
    have H : Â¬p âˆ£ j - i := Nat.not_dvd_of_pos_of_lt (by lia) (by lia)
    exact Finset.dvd_prod_of_mem _ (by simp [Nat.sub_lt_of_lt, hp.coprime_iff_not_dvd, H])
  Â· exact hÎ¶.isUnit (by simp [cyclotomic_coeff_zero _ hp.one_lt])

lemma RingHom.IsIntegralElem.map {R S T : Type*} [CommRing R] [CommRing S] [CommRing T]
    {f : R â†’+* S} {x : S} (hx : f.IsIntegralElem x) (g : S â†’+* T) :
    (g.comp f).IsIntegralElem (g x) := by
  obtain âŸ¨p, hp, hxâŸ© := hx
  exact âŸ¨p, hp, by simp_rw [â† hom_evalâ‚‚, evalâ‚‚_eq_eval_map] at hx âŠ¢; simp [hx]âŸ©

lemma RingHom.IsIntegralElem.of_comp {R S T : Type*} [CommRing R] [CommRing S] [CommRing T]
    {f : R â†’+* S} {g : S â†’+* T} {x : T} (hx : (g.comp f).IsIntegralElem x) :
    g.IsIntegralElem x := by
  obtain âŸ¨p, hp, hxâŸ© := hx
  exact âŸ¨p.map f, hp.map _, by simpa only [evalâ‚‚_eq_eval_map, Polynomial.map_map] using hxâŸ©

open TensorProduct in
attribute [local instance] Polynomial.algebra in
set_option maxHeartbeats 0 in
theorem isIntegral_coeff_of_isIntegral_aux
    (p : â„•) (hp : p.Prime) (hp' : IsUnit (p : R)) {f : S[X]} (hfp : f.natDegree < p)
    (hf : IsIntegral R[X] f)
    (n : â„•) : IsIntegral R (f.coeff n) := by
  classical
  nontriviality R
  nontriviality S
  let R' := AdjoinRoot (cyclotomic p R)
  have inst : Module.Finite R R' := (AdjoinRoot.powerBasis' (cyclotomic.monic p R)).finite
  have inst : Module.Free R R' := .of_basis (AdjoinRoot.powerBasis' (cyclotomic.monic p R)).basis
  have : Nontrivial (S âŠ—[R] R') := by
    have : Nonempty (Fin (cyclotomic p R).natDegree) :=
      Fin.pos_iff_nonempty.mp (by simp [natDegree_cyclotomic, hp.pos])
    have := ((AdjoinRoot.powerBasis' (cyclotomic.monic p R)).basis.baseChange S).repr
    rw [AdjoinRoot.powerBasis'_dim] at this
    exact this.nontrivial
  have : Nontrivial (R' âŠ—[R] S) := (Algebra.TensorProduct.comm _ _ _).nontrivial
  let Î¶ : R' := .root _
  have hÎ¶â‚€ : aeval Î¶ (cyclotomic p R) = 0 :=
        ((AdjoinRoot.aeval_eq (cyclotomic p R) (f := cyclotomic p R)).trans AdjoinRoot.mk_self)
  have hÎ¶â‚€' : (cyclotomic p R').IsRoot Î¶ := by rwa [â† eval_map_algebraMap, map_cyclotomic] at hÎ¶â‚€
  let f' : (R' âŠ—[R] S)[X] := f.map Algebra.TensorProduct.includeRight.toRingHom
  let Î¶' : R' âŠ—[R] S := Î¶ âŠ—â‚œ 1
  have hÎ¶'â‚€ : (cyclotomic p (R' âŠ—[R] S)).IsRoot Î¶' := by
    simpa using hÎ¶â‚€'.map (f := algebraMap R' (R' âŠ—[R] S))
  have Hsub (i j : Fin p) (hij : i â‰  j) : IsUnit (Î¶ ^ i.1 - Î¶ ^ j.1) :=
    isUnit_pow_sub_pow_of_isRoot_cyclotomic hp (by simpa [R'] using hp'.map (algebraMap _ _))
      hÎ¶â‚€' _ _ hij
  have Hsub' (i j : Fin p) (hij : i â‰  j) : IsUnit (Î¶' ^ i.1 - Î¶' ^ j.1) :=
    isUnit_pow_sub_pow_of_isRoot_cyclotomic hp (by simpa [R'] using hp'.map (algebraMap _ _))
      hÎ¶'â‚€ _ _ hij
  have hÎ¶' : IsPrimitiveRoot Î¶' p := by
    refine .mk_of_natPrime hp ?_ ?_
    Â· have := aeval_eq_zero_of_dvd_aeval_eq_zero (cyclotomic.dvd_X_pow_sub_one _ _) hÎ¶'â‚€
      simpa [sub_eq_zero, Î¶'] using this
    Â· intro e
      have := Fact.mk hp
      have : (p : R' âŠ—[R] S) = 0 := by simpa [e, cyclotomic_prime, R'] using hÎ¶'â‚€
      simpa [this, R'] using (hp'.map (algebraMap R (R' âŠ—[R] S))).ne_zero
  have : f' - âˆ‘ i âˆˆ (Finset.range p).attach, C (f'.eval (Î¶' ^ i.1)) *
      âˆ j âˆˆ ((Finset.range p).erase i).attach, (X - C (Î¶' ^ j.1)) *
        C ((Hsub' âŸ¨i.1, by grindâŸ© âŸ¨j.1, by grindâŸ© (by grind)).unitâ»Â¹).1 = 0 := by
    by_contra H
    refine H (Polynomial.eq_zero_of_degree_lt_of_eval_eq_zero
      ((Finset.range p).image (Î¶' ^ Â·)) ?_ ?_ ?_)
    Â· rw [â† Polynomial.natDegree_lt_iff_degree_lt H]
      refine (natDegree_sub_le _ _).trans_lt (max_lt (natDegree_map_le.trans_lt ?_) ?_)
      Â· rwa [Finset.card_image_of_injOn hÎ¶'.injOn_pow, Finset.card_range]
      Â· refine (natDegree_sum_le _ _).trans_lt ((Finset.fold_max_lt _).mpr ?_)
        simp only [Finset.card_pos, Finset.image_nonempty, Finset.nonempty_range_iff, ne_eq,
          hp.ne_zero, not_false_eq_true, Finset.mem_attach, Function.comp_apply,
          forall_const, Subtype.forall, Finset.mem_range, true_and]
        intro i hip
        grw [natDegree_C_mul_le, natDegree_prod_le]
        refine (Finset.sum_le_sum fun _ _ â†¦ (natDegree_mul_C_le _ _).trans
          (natDegree_X_sub_C_le _)).trans_lt ?_
        simpa [Finset.card_image_of_injOn hÎ¶'.injOn_pow] using
          Finset.card_erase_lt_of_mem (Finset.mem_range.mpr hip)
    Â· simp only [Finset.mem_image, Finset.mem_range, map_pow, eval_sub, eval_finset_sum, eval_mul,
        eval_C, eval_prod, eval_X, eval_pow, sub_eq_zero, forall_exists_index, and_imp,
        forall_apply_eq_imp_iffâ‚‚]
      intro i hip
      rw [Finset.sum_eq_single âŸ¨i, Finset.mem_range.mpr hipâŸ©]
      Â· simp
      Â· simp only [Finset.mem_attach, ne_eq, forall_const, Subtype.forall, Finset.mem_range,
          Subtype.mk.injEq]
        intro j hjp hji
        rw [Finset.prod_eq_zero (Finset.mem_attach _ âŸ¨i, by grindâŸ©), mul_zero]
        simp
      Â· simp
    Â· rw [Finset.coe_image, Set.InjOn.pairwise_image hÎ¶'.injOn_pow, Finset.coe_range]
      refine fun i hi j hj hij â†¦ (Hsub' âŸ¨i, hiâŸ© âŸ¨j, hjâŸ© (by simpa)).mem_nonZeroDivisors
  have hf' : IsIntegral R'[X] f' := by
    let Ï† : S[X] â†’â‚[R[X]] (R' âŠ—[R] S)[X] :=
      âŸ¨mapRingHom Algebra.TensorProduct.includeRight.toRingHom,
        fun r â†¦ by simp [Polynomial.map_map]âŸ©
    have : IsScalarTower R[X] R'[X] (R' âŠ—[R] S)[X] := .of_algebraMap_eq fun r â†¦
      by simp [Polynomial.map_map, â† IsScalarTower.algebraMap_eq]
    exact (hf.map Ï†).tower_top
  have : IsIntegral R' (f'.coeff n) := by
    rw [sub_eq_zero.mp this, finset_sum_coeff]
    refine .sum _ fun i hi â†¦ ?_
    rw [coeff_C_mul]
    refine .mul ?_ ?_
    Â· refine RingHom.IsIntegralElem.of_comp (f := evalRingHom (Î¶ ^ i.1)) ?_
      convert RingHom.IsIntegralElem.map hf' (evalRingHom (Î¶' ^ i.1))
      ext <;> simp [R', Î¶']
    Â· refine isIntegral_coeff_prod _ _ (fun âŸ¨j, hjâŸ© _ k â†¦ ?_) _
      obtain âŸ¨hjâ‚, hjâ‚‚âŸ© : j â‰  â†‘i âˆ§ j < p := by simpa using hj
      rw [coeff_mul_C]
      refine .mul ?_ ?_
      Â· simp only [coeff_sub, coeff_X, coeff_C]
        split_ifs
        Â· exact .sub isIntegral_one
            (by simpa [Î¶'] using isIntegral_algebraMap (A := R' âŠ—[R] S) (x := Î¶ ^ j))
        Â· simpa using isIntegral_one
        Â· simpa [Î¶'] using isIntegral_algebraMap (A := R' âŠ—[R] S) (x := Î¶ ^ j)
        Â· simpa using isIntegral_zero
      Â· convert isIntegral_algebraMap
          (x := (Hsub âŸ¨i, Finset.mem_range.mp i.2âŸ© âŸ¨j, hjâ‚‚âŸ© (by simp [hjâ‚.symm])).unitâ»Â¹.1)
        convert_to _ = (((Hsub âŸ¨i, Finset.mem_range.mp i.2âŸ© âŸ¨j, hjâ‚‚âŸ©
          (by simp [hjâ‚.symm]))).unit.map (algebraMap R' (R' âŠ—[R] S)).toMonoidHom)â»Â¹.1
        congr 2; ext; simp [Î¶', sub_tmul]
  have inst : FaithfulSMul S (S âŠ—[R] R') := Module.Free.instFaithfulSMulOfNontrivial _ _
  have : IsIntegral R (1 âŠ—â‚œ[R] f.coeff n : R' âŠ—[R] S) := by simpa [f'] using isIntegral_trans _ this
  exact (this.map (Algebra.TensorProduct.comm _ _ _).toAlgHom).tower_bot (A := S)
    (FaithfulSMul.algebraMap_injective _ _)

open TensorProduct in
attribute [local instance] Polynomial.algebra in
theorem isIntegral_coeff_of_isIntegral {f : S[X]} (hf : IsIntegral R[X] f)
    (n : â„•) : IsIntegral R (f.coeff n) := by
  obtain âŸ¨p, hfp, hpâŸ© := (f.natDegree + 1).exists_infinite_primes
  obtain âŸ¨q, hpq, hqâŸ© := (p + 1).exists_infinite_primes
  have (p : â„•) (hp : p.Prime) (hp' : f.natDegree < p) : âˆƒ i, IsIntegral R (p ^ i * f.coeff n) := by
    let := (Localization.awayMap (algebraMap R S) p).toAlgebra
    have : IsScalarTower R (Localization.Away (p : R)) (Localization.Away (algebraMap R S p)) :=
      .of_algebraMap_eq fun r â†¦ by simp [RingHom.algebraMap_toAlgebra, Localization.awayMap,
        IsLocalization.Away.map, â† IsScalarTower.algebraMap_apply]
    have := isIntegral_coeff_of_isIntegral_aux p hp (R := Localization.Away (p : R))
      (by simpa using IsLocalization.Away.algebraMap_isUnit (p : R))
      (S := Localization.Away (algebraMap R S p)) (f := f.map (algebraMap _ _))
      (by grw [natDegree_map_le]; lia) (by
        let Ï† : S[X] â†’â‚[R[X]] (Localization.Away (algebraMap R S p))[X] :=
          âŸ¨mapRingHom (algebraMap _ _),
            fun r â†¦ by simp [Polynomial.map_map, â† IsScalarTower.algebraMap_eq]âŸ©
        have : IsScalarTower R[X] (Localization.Away (p : R))[X]
          (Localization.Away (algebraMap R S p))[X] :=
          .of_algebraMap_eq fun r â†¦
          by simp [Polynomial.map_map, â† IsScalarTower.algebraMap_eq]
        exact (hf.map Ï†).tower_top) n
    obtain âŸ¨âŸ¨_, i, rflâŸ©, hiâŸ© := this.exists_multiple_integral_of_isLocalization (.powers (p : R)) _
    obtain âŸ¨_, âŸ¨j, rflâŸ©, hjâŸ© := IsLocalization.exists_isIntegral_smul_of_isIntegral_map
      (M := .powers (p : R)) (Sâ‚˜ := Localization.Away (algebraMap R S p))
      (x := p ^ i * f.coeff n) (by simpa [Submonoid.smul_def, Algebra.smul_def] using hi)
    exact âŸ¨j + i, by simpa [Algebra.smul_def, pow_add, mul_assoc] using hjâŸ©
  obtain âŸ¨i, hiâŸ© := this p hp hfp
  obtain âŸ¨j, hjâŸ© := this q hq (by lia)
  have : q.Coprime p := hq.coprime_iff_not_dvd.mpr (Nat.not_dvd_of_pos_of_lt hp.pos hpq)
  obtain âŸ¨a, b, eâŸ© := ((this.pow_left j).pow_right i).isCoprime
  replace e : (â†‘a * â†‘q ^ j + â†‘b * â†‘p ^ i : S) = 1 := by simpa using congr(($e : S))
  have := (hj.smul a).add (hi.smul b)
  simpa [â† mul_assoc, â† add_mul, e] using this

attribute [local instance] Polynomial.algebra in
theorem isIntegral_iff_isIntegral_coeff {f : S[X]} :
    IsIntegral R[X] f â†” âˆ€ n, IsIntegral R (f.coeff n) := by
  refine âŸ¨isIntegral_coeff_of_isIntegral, fun H â†¦ ?_âŸ©
  rw [â† f.sum_monomial_eq, Polynomial.sum]
  simp only [â† C_mul_X_pow_eq_monomial, â† map_X (algebraMap R S)]
  exact .sum _ fun i _ â†¦ ((H i).map (CAlgHom (R := R))).tower_top.mul (.pow isIntegral_algebraMap _)

variable {B : Type*} [CommRing B] [Algebra R B]

lemma IsIntegral.of_aeval_monic_of_isIntegral_coeff {R A : Type*} [CommRing R] [CommRing A]
    [Algebra R A] {x : A} {p : A[X]} (monic : p.Monic) (deg : p.natDegree â‰  0)
    (hx : IsIntegral R (eval x p)) (hp : âˆ€ i, IsIntegral R (p.coeff i)) : IsIntegral R x := by
  obtain âŸ¨q, hqp, hdeg, hqâŸ© :=
    lifts_and_natDegree_eq_and_monic (p := p) (f := algebraMap (integralClosure R A) _)
    (p.lifts_iff_coeff_lifts.mpr (by simpa)) monic
  exact isIntegral_trans _ (.of_aeval_monic hq (hdeg â–¸ deg)
    (by simpa [â† eval_map_algebraMap, hqp] using hx.tower_top))

attribute [local instance] Polynomial.algebra in
/-- Let `S` be an `R`-algebra and `f : S[X]` be a monic polynomial with `R`-integral coefficients.
Suppose `y` in `B = S[X]/f` is `R`-integral, then `f' * y` is the image of some `g : S[X]` with
`R`-integral coefficients. -/
-- We can also know that `deg g = deg f - 1`. Upgrade the lemma if we care.
lemma exists_derivative_mul_eq_and_isIntegral_coeff
    {Ï† : S[X] â†’â‚[R] B} (hÏ† : Function.Surjective Ï†) {f : S[X]} (hf : f.Monic)
    (hf' : âˆ€ i, IsIntegral R (f.coeff i))
    (hfx : RingHom.ker Ï†.toRingHom = .span {f}) {y : B} (hy : IsIntegral R y) :
    âˆƒ (g : S[X]), Ï† f.derivative * y = Ï† g âˆ§ âˆ€ i, IsIntegral R (g.coeff i) := by
  cases subsingleton_or_nontrivial B
  Â· exact âŸ¨0, Subsingleton.elim _ _, by simp [isIntegral_zero]âŸ©
  have hfd : f.natDegree â‰  0 := by
    rw [ne_eq, hf.natDegree_eq_zero]
    rintro rfl
    simpa using (RingHom.ker_ne_top Ï†.toRingHom).symm.trans_eq hfx
  classical
  let := (Ï†.toRingHom.comp C).toAlgebra
  have : IsScalarTower R S B := .of_algebraMap_eq' (Ï†.comp CAlgHom).comp_algebraMap.symm
  have := (algebraMap S B).domain_nontrivial
  obtain âŸ¨y, rflâŸ© := hÏ† y
  let S' := f.SplittingAlgebra
  have hS' : (f.map (algebraMap S S')).Splits := splits_splittingAlgebra _ hf
  obtain âŸ¨m, hmâŸ© := Polynomial.splits_iff_exists_multiset.mp hS'
  simp only [hf.map _, Monic.leadingCoeff, map_one, one_mul] at hm
  algebraize [(algebraMap S S').comp (algebraMap R S)]
  have hm' : âˆ€ a âˆˆ m, IsIntegral R a := by
    refine fun a ham â†¦ .of_aeval_monic_of_isIntegral_coeff (hf.map (algebraMap _ _)) ?_ ?_ ?_
    Â· rwa [hf.natDegree_map]
    Â· rw [hm, eval_multiset_prod, Multiset.prod_eq_zero]
      Â· exact isIntegral_zero
      Â· simpa using âŸ¨a, ham, by simpâŸ©
    Â· simp only [coeff_map]
      exact fun _ â†¦ (hf' _).algebraMap
  have hmc : m.card = f.natDegree := by
    simpa [hf.natDegree_map, natDegree_multiset_prod_of_monic] using congr(($hm).natDegree).symm
  have H : (f.derivative * y %â‚˜ f).map (algebraMap S S') =
        (m.map fun x â†¦ ((m.erase x).map (X - C Â·)).prod * C (aeval x y)).sum := by
    have âŸ¨g, hgâŸ© : f.map (algebraMap _ _) âˆ£ (f.derivative * y).map (algebraMap S S') -
        (m.map fun x â†¦ ((m.erase x).map (X - C Â·)).prod * C (aeval x y)).sum := by
      rw [Polynomial.map_mul, â† Polynomial.derivative_map, hm, derivative_prod,
        â† Multiset.sum_map_mul_right, â† Multiset.sum_map_sub]
      refine Multiset.dvd_sum ?_
      simp only [derivative_sub, derivative_X, derivative_C, sub_zero, mul_one, â† mul_sub,
        Multiset.mem_map, forall_exists_index, and_imp, forall_apply_eq_imp_iffâ‚‚]
      intro a ham
      conv_lhs => rw [â† Multiset.cons_erase ham]
      rw [Multiset.map_cons, Multiset.prod_cons, mul_comm]
      refine mul_dvd_mul_left _ ?_
      rw [Polynomial.dvd_iff_isRoot]
      simp
    rw [map_modByMonic _ hf]
    refine (div_modByMonic_unique g _ (hf.map _) âŸ¨(sub_eq_iff_eq_add'.mp hg).symm, ?_âŸ©).2
    refine degree_lt_degree ?_
    rw [hf.natDegree_map, â† Nat.le_sub_one_iff_lt (Ne.bot_lt hfd)]
    refine (natDegree_multiset_sum_le _).trans (Multiset.max_le_of_forall_le _ _ ?_)
    simp only [Multiset.map_map, Function.comp_apply, Multiset.mem_map, forall_exists_index,
      and_imp, forall_apply_eq_imp_iffâ‚‚]
    refine fun a ha â†¦ (natDegree_mul_C_le _ _).trans ((natDegree_multiset_prod_le _).trans ?_)
    simp [ha, hmc]
  have : IsScalarTower R[X] S[X] S'[X] := .of_algebraMap_eq' (mapRingHom_comp ..).symm
  have H' : IsIntegral R[X] (f.derivative * y %â‚˜ f) := by
    refine .tower_bot (B := S'[X]) (map_injective _ (FaithfulSMul.algebraMap_injective S S')) ?_
    simp only [algebraMap_def, coe_mapRingHom, H]
    refine .multiset_sum ?_
    simp only [Multiset.mem_map, forall_exists_index, and_imp, forall_apply_eq_imp_iffâ‚‚]
    refine fun a ham â†¦ .mul (.multiset_prod ?_) ?_
    Â· simp only [Multiset.mem_map, isIntegral_iff_isIntegral_coeff, forall_exists_index, and_imp,
        forall_apply_eq_imp_iffâ‚‚, coeff_sub]
      exact fun b hbm n â†¦ .sub (by simp [coeff_X, apply_ite, isIntegral_one, isIntegral_zero])
        (by simp [coeff_C, apply_ite, isIntegral_zero, hm' b (Multiset.mem_of_mem_erase hbm)])
    Â· let Ïˆ : B â†’â‚[R] S' := AlgHom.liftOfSurjective _ hÏ† ((aeval a).restrictScalars R) <| by
        rw [hfx, Ideal.span_le]
        suffices (m.map (a - Â·)).prod = 0 by simpa [â† eval_map_algebraMap, hm, eval_multiset_prod]
        rw [Multiset.prod_eq_zero]
        simpa using âŸ¨a, ham, by simpâŸ©
      simpa [isIntegral_iff_isIntegral_coeff, coeff_C, apply_ite, isIntegral_zero, Ïˆ] using hy.map Ïˆ
  refine âŸ¨_, ?_, isIntegral_iff_isIntegral_coeff.mp H'âŸ©
  rw [modByMonic_eq_sub_mul_div _ hf, map_sub, map_mul, map_mul,
    show Ï† f = 0 from hfx.ge (Ideal.mem_span_singleton_self _), zero_mul, sub_zero]

lemma Polynomial.Monic.leadingCoeff_C_mul {R : Type*} [CommRing R] {p : R[X]}
    (hp : p.Monic) (r : R) : (C r * p).leadingCoeff = r := by
  by_cases hr : r = 0; Â· simp_all
  rw [â† Polynomial.coeff_natDegree, natDegree_C_mul_of_mul_ne_zero (by simp_all), coeff_C_mul,
    hp.coeff_natDegree, mul_one]

/-- If `t` is `R`-integral in `S[Mâ»Â¹]` where `M` is a submonoid of `R`,
then `m â€¢ t` is integral in `S` for some `m âˆˆ M`. -/
lemma IsLocalization.Away.exists_isIntegral_smul_of_isIntegral_map
    {R S Sâ‚˜ : Type*} [CommRing R] [CommRing S] [CommRing Sâ‚˜] [Algebra R S] [Algebra S Sâ‚˜]
    [Algebra R Sâ‚˜] [IsScalarTower R S Sâ‚˜] {r : S} (hr : IsIntegral R r)
    [IsLocalization.Away r Sâ‚˜] {x : S}
    (hx : IsIntegral R (algebraMap S Sâ‚˜ x)) : âˆƒ n, IsIntegral R (r ^ n * x) := by
  nontriviality S
  obtain âŸ¨p, hpm, hpâŸ© := hx
  simp only [IsScalarTower.algebraMap_eq R S Sâ‚˜, â† hom_evalâ‚‚,
    IsLocalization.map_eq_zero_iff (.powers r), Subtype.exists, Submonoid.mem_powers_iff,
    exists_prop, exists_exists_eq_and] at hp
  obtain âŸ¨m, hmâŸ© := hp
  have := isIntegral_trans (R := R) _ (isIntegral_leadingCoeff_smul (R := integralClosure R S)
    (C âŸ¨r, hrâŸ© ^ m * p.map (algebraMap _ _)) x (by simpa [â† aeval_def] using hm))
  rw [â† map_pow, (hpm.map _).leadingCoeff_C_mul] at this
  exact âŸ¨m, thisâŸ©

open TensorProduct

attribute [local instance] Polynomial.algebra in
theorem mem_adjoin_map_integralClosure_of_isStandardEtale
    {B : Type*} [CommRing B] [Algebra R B] [Algebra.IsStandardEtale R S]
    (x : S âŠ—[R] B) (hx : IsIntegral S x) :
    x âˆˆ Algebra.adjoin S
      ((integralClosure R B).map Algebra.TensorProduct.includeRight : Subalgebra R (S âŠ—[R] B)) := by
  have ğ“Ÿ := Classical.ofNonempty (Î± := StandardEtalePresentation R S)
  obtain âŸ¨n, hxâŸ© : âˆƒ n, âˆ€ m, IsIntegral R ((aeval ğ“Ÿ.x ğ“Ÿ.g) ^ (n + m) â€¢ x) := by
    let e := ğ“Ÿ.equivRing.trans ğ“Ÿ.equivAwayAdjoinRoot
    let := (e.symm.toAlgHom.comp (IsScalarTower.toAlgHom R (AdjoinRoot ğ“Ÿ.f) _)).toAlgebra
    have := IsScalarTower.of_algebraMap_eq'
      (e.symm.toAlgHom.comp (IsScalarTower.toAlgHom R (AdjoinRoot ğ“Ÿ.f) _)).comp_algebraMap.symm
    have := IsLocalization.isLocalization_of_algEquiv (R := AdjoinRoot ğ“Ÿ.f) (.powers (.mk _ ğ“Ÿ.g))
      { toRingEquiv := e.symm.toRingEquiv, commutes' := by simp [RingHom.algebraMap_toAlgebra] }
    obtain âŸ¨âŸ¨_, m, rflâŸ©, hmâŸ© := IsIntegral.exists_multiple_integral_of_isLocalization
      (R := AdjoinRoot ğ“Ÿ.f) (.powers (.mk _ ğ“Ÿ.g)) _ hx
    replace hm := fun k : â„• â†¦ (isIntegral_algebraMap (x := AdjoinRoot.mk ğ“Ÿ.f ğ“Ÿ.g ^ k)).mul hm
    simp only [Submonoid.smul_def, â† @IsScalarTower.algebraMap_smul (AdjoinRoot ğ“Ÿ.f) S,
      â† map_pow, â† Algebra.smul_def, â† mul_smul, â† map_mul, â† pow_add, add_comm _ m] at hm
    simp_rw [map_pow] at hm
    have := ğ“Ÿ.monic_f.finite_adjoinRoot
    suffices algebraMap (AdjoinRoot ğ“Ÿ.f) S (.mk _ ğ“Ÿ.g) = aeval ğ“Ÿ.x ğ“Ÿ.g from
      âŸ¨m, fun k â†¦ this â–¸ isIntegral_trans (R := R) _ (hm k)âŸ©
    simp [RingHom.algebraMap_toAlgebra, e, StandardEtalePair.equivAwayAdjoinRoot,
      â† aeval_def, â† aeval_algHom_apply]
  let ğ“Ÿ' := ğ“Ÿ.baseChange (A := B)
  let e := ğ“Ÿ'.equivRing.trans ğ“Ÿ'.equivAwayAdjoinRoot
  obtain âŸ¨x, rflâŸ© := (Algebra.TensorProduct.comm _ _ _).surjective x
  obtain âŸ¨x, rflâŸ© := e.symm.surjective x
  obtain âŸ¨x, âŸ¨_, m, rflâŸ©, rflâŸ© := IsLocalization.exists_mk'_eq
    (R := AdjoinRoot ğ“Ÿ'.f) (.powers (.mk _ ğ“Ÿ'.g)) x
  have hfg : IsIntegral R (AdjoinRoot.mk ğ“Ÿ'.f ğ“Ÿ'.g) := by
    have := ğ“Ÿ.monic_f.finite_adjoinRoot
    let e : AdjoinRoot ğ“Ÿ.f â†’â‚[R] AdjoinRoot ğ“Ÿ'.f :=
      AdjoinRoot.mapAlgHom (Algebra.ofId _ _) _ _ (dvd_refl _)
    convert (Algebra.IsIntegral.isIntegral (R := R) (AdjoinRoot.mk ğ“Ÿ.f ğ“Ÿ.g)).map e
    have : (AdjoinRoot.mk ğ“Ÿ'.f).comp (mapRingHom (algebraMap R B)) =
        e.toRingHom.comp (AdjoinRoot.mk _) := by ext <;> simp [e]
    exact congr($this ğ“Ÿ.g)
  have heg (g : R[X]) : e (1 âŠ—â‚œ aeval ğ“Ÿ.x g) =
      algebraMap _ _ (AdjoinRoot.mk ğ“Ÿ'.f (g.map (algebraMap _ _))) := by
    trans e (aeval (1 âŠ—â‚œ ğ“Ÿ.x) (g.map (algebraMap _ B)))
    Â· rw [â† Algebra.TensorProduct.includeRight_apply, â† aeval_algHom_apply]
      simp [StandardEtalePresentation.baseChange, ğ“Ÿ']
    rw [â† e.eq_symm_apply]
    simp [e, StandardEtalePair.equivAwayAdjoinRoot, â† aeval_def, â† aeval_algHom_apply]
    rfl
  obtain âŸ¨k, hkâŸ© : âˆƒ k, IsIntegral R (AdjoinRoot.mk ğ“Ÿ'.f ğ“Ÿ'.g ^ k * x) := by
    have H : âˆ€ k, e (1 âŠ—â‚œ (aeval ğ“Ÿ.x ğ“Ÿ.g ^ k)) = algebraMap _ _ (AdjoinRoot.mk ğ“Ÿ'.f ğ“Ÿ'.g ^ k) := by
      intro k; convert congr($(heg ğ“Ÿ.g) ^ k) <;>
        simp [â† map_pow, ğ“Ÿ', StandardEtalePresentation.baseChange]
    have := ((hx m).map (Algebra.TensorProduct.comm _ _ _).symm).map e
    simp only [Algebra.smul_def, Algebra.TensorProduct.algebraMap_apply, Algebra.algebraMap_self,
      RingHom.id_apply, map_mul, Algebra.TensorProduct.comm_symm_tmul, AlgEquiv.symm_apply_apply,
      AlgEquiv.apply_symm_apply] at this
    rw [H, pow_add, map_mul, mul_assoc, IsLocalization.mk'_spec'_mk, â† map_mul] at this
    obtain âŸ¨k, hkâŸ© := IsLocalization.Away.exists_isIntegral_smul_of_isIntegral_map hfg this
    refine âŸ¨k + n, by convert hk using 1; ring_nfâŸ©
  obtain âŸ¨y, hy, hRyâŸ© := exists_derivative_mul_eq_and_isIntegral_coeff
    (Ï† := (AdjoinRoot.mkâ‚ ğ“Ÿ'.f).restrictScalars R) AdjoinRoot.mk_surjective ğ“Ÿ'.monic_f
    (by simp [ğ“Ÿ', StandardEtalePresentation.baseChange, isIntegral_algebraMap]) Ideal.mk_ker hk
  simp only [AlgHom.coe_restrictScalars', AdjoinRoot.coe_mkâ‚] at hy
  rw [â† Subalgebra.mem_toSubmodule, â† Submodule.smul_mem_iff_of_isUnit _
    (ğ“Ÿ.hasMap.isUnit_derivative_f.mul <| (ğ“Ÿ.hasMap.2.pow k).mul (ğ“Ÿ.hasMap.2.pow m))]
  convert_to evalâ‚‚ Algebra.TensorProduct.includeRight.toRingHom (ğ“Ÿ.x âŠ—â‚œ[R] 1) y âˆˆ _ using 1
  Â· convert congr(Algebra.TensorProduct.comm _ _ _ <| e.symm (algebraMap _ _ $hy))
    Â· apply (Algebra.TensorProduct.comm R B S).symm.injective
      apply e.injective
      simp only [Algebra.smul_def, Algebra.TensorProduct.algebraMap_apply, Algebra.algebraMap_self,
        RingHom.id_apply, map_mul, Algebra.TensorProduct.comm_symm_tmul, AlgEquiv.symm_apply_apply,
        AlgEquiv.apply_symm_apply, map_pow, heg]
      simp_rw [mul_assoc, â† map_pow, show ğ“Ÿ.g.map (algebraMap R B) = ğ“Ÿ'.g from rfl,
        IsLocalization.mk'_spec'_mk, â† derivative_map]; rfl
    Â· simp only [â† AlgEquiv.coe_algHom, â† AlgHom.coe_toRingHom, â† RingHom.comp_apply,
        â† coe_evalâ‚‚RingHom]
      congr 1
      ext <;> simp [e, StandardEtalePair.equivAwayAdjoinRoot]; rfl
  Â· rw [evalâ‚‚_eq_sum_range]
    exact sum_mem fun i hi â†¦ Subalgebra.mul_mem _ (Algebra.subset_adjoin âŸ¨_, hRy _, rflâŸ©)
      (pow_mem (Subalgebra.algebraMap_mem _ _) _)

attribute [local instance high] AlgHomClass.toRingHomClass RingHomClass.toAddMonoidHomClass
  AddMonoidHomClass.toAddHomClass in
variable (R S) in
def TensorProduct.toIntegralClosure
    (B : Type*) [CommRing B] [Algebra R B] :
    S âŠ—[R] integralClosure R B â†’â‚[S] integralClosure S (S âŠ—[R] B) :=
    (Algebra.TensorProduct.map (.id _ _) (integralClosure R B).val).codRestrict _ fun x â†¦ by
  induction x with
  | zero => simp
  | add x y _ _ => rw [map_add]; exact add_mem â€¹_â€º â€¹_â€º
  | tmul x y =>
    convert ((y.2.map (Algebra.TensorProduct.includeRight
      (R := R) (A := S))).tower_top (A := S)).smul x
    simp [smul_tmul']

open TensorProduct

instance (priority := low) {R A B : Type*} [CommSemiring A] [Semiring B] [Algebra A B]
    (s : Subalgebra A B) [Semiring R] [SMul R A] [Module R B] [IsScalarTower R A B] :
    IsScalarTower R s B :=
  .toâ‚â‚ƒâ‚„ _ A _ _

instance (priority := low) {R S A B : Type*} [CommSemiring A] [Semiring B] [Algebra A B]
    (s : Subalgebra A B) [Semiring R] [SMul R A] [Module R B] [IsScalarTower R A B]
    [Semiring S] [SMul S A] [Module S B] [IsScalarTower S A B] [SMul R S] [IsScalarTower R S B] :
    IsScalarTower R S s :=
  .toâ‚â‚‚â‚ƒ _ _ _ B

lemma Algebra.IsPushout.tensorProduct_tensorProduct
    (R S A B : Type*) [CommRing R] [CommRing S] [CommRing A] [CommRing B]
    [Algebra R A] [Algebra R B] [Algebra A B] [IsScalarTower R A B] [Algebra R S]
    {_ : Algebra (A âŠ—[R] S) (B âŠ—[R] S)} {_ : IsScalarTower A (A âŠ—[R] S) (B âŠ—[R] S)}
    (H : (algebraMap (A âŠ—[R] S) (B âŠ—[R] S)).comp Algebra.TensorProduct.includeRight.toRingHom =
      Algebra.TensorProduct.includeRight.toRingHom) :
    Algebra.IsPushout A B (A âŠ—[R] S) (B âŠ—[R] S) := by
  constructor
  convert isBaseChange_tensorProduct_map (R := R) (P := S) _ (IsBaseChange.linearMap A B)
  ext s
  simpa using congr($H s)

lemma IsLocalization.tensorProduct_tensorProduct
    (R S : Type*) [CommRing R] [CommRing S] {A : Type*} [CommRing A] (M : Submonoid A)
    (B : Type*) [CommRing B] [Algebra R A] [Algebra R B] [Algebra A B] [IsScalarTower R A B]
    [Algebra R S] [IsLocalization M B]
    [Algebra (A âŠ—[R] S) (B âŠ—[R] S)] [IsScalarTower A (A âŠ—[R] S) (B âŠ—[R] S)]
    (H : (algebraMap (A âŠ—[R] S) (B âŠ—[R] S)).comp Algebra.TensorProduct.includeRight.toRingHom =
      Algebra.TensorProduct.includeRight.toRingHom) :
    IsLocalization (Algebra.algebraMapSubmonoid (A âŠ—[R] S) M) (B âŠ—[R] S) :=
  (Algebra.isLocalization_iff_isPushout M _).mpr
    (Algebra.IsPushout.tensorProduct_tensorProduct R S A B H).symm

set_option synthInstance.maxHeartbeats 0 in
set_option maxHeartbeats 0 in
-- set_option trace.profiler true in
lemma TensorProduct.toIntegralClosure_bijective_of_isLocalizationAway
    {s : Set S} (hs : Ideal.span s = âŠ¤) (Sáµ£ : s â†’ Type*) [âˆ€ r, CommRing (Sáµ£ r)]
    [âˆ€ r, Algebra S (Sáµ£ r)] [âˆ€ r, Algebra R (Sáµ£ r)] [âˆ€ r, IsScalarTower R S (Sáµ£ r)]
    [âˆ€ r, IsLocalization.Away r.1 (Sáµ£ r)]
    (H : âˆ€ r, Function.Bijective (toIntegralClosure R (Sáµ£ r) B)) :
    Function.Bijective (toIntegralClosure R S B) := by
  have (r : s) : IsLocalizedModule.Away r.1
      (Algebra.TensorProduct.map (Algebra.ofId S (Sáµ£ r))
        (AlgHom.id R (integralClosure R B))).toLinearMap := by
    let := (Algebra.TensorProduct.map (Algebra.ofId S (Sáµ£ r))
      (AlgHom.id R (integralClosure R B))).toAlgebra
    refine isLocalizedModule_iff_isLocalization.mpr ?_
    refine IsLocalization.tensorProduct_tensorProduct _ _ (.powers r.1) _ ?_
    ext; simp [RingHom.algebraMap_toAlgebra]
  let Ï† (r : s) : integralClosure S (S âŠ—[R] B) â†’â‚[S] integralClosure (Sáµ£ r) (Sáµ£ r âŠ—[R] B) :=
    ((Algebra.TensorProduct.map (Algebra.ofId _ _) (.id _ _)).comp
      (integralClosure S (S âŠ—[R] B)).val).codRestrict
        ((integralClosure (Sáµ£ r) (Sáµ£ r âŠ—[R] B)).restrictScalars S) <| by
    simp only [AlgHom.coe_comp, Subalgebra.coe_val, Function.comp_apply,
      Subalgebra.mem_restrictScalars, Subtype.forall, mem_integralClosure_iff]
    exact fun a ha â†¦ (ha.map _).tower_top
  have (r : s) : IsLocalizedModule.Away r.1 (Ï† r).toLinearMap := by
    let := (Algebra.TensorProduct.map (Algebra.ofId S (Sáµ£ r))
          (AlgHom.id R B)).toAlgebra
    let := (Ï† r).toAlgebra
    have : IsScalarTower (integralClosure S (S âŠ—[R] B)) (integralClosure (Sáµ£ r) (Sáµ£ r âŠ—[R] B))
        (Sáµ£ r âŠ—[R] B) := .of_algebraMap_eq' rfl
    have : IsLocalization (Algebra.algebraMapSubmonoid (S âŠ—[R] B) (Submonoid.powers r.1))
        (Sáµ£ r âŠ—[R] B) := by
      refine IsLocalization.tensorProduct_tensorProduct _ _ (.powers r.1) _ ?_
      ext; simp [RingHom.algebraMap_toAlgebra]
    refine isLocalizedModule_iff_isLocalization.mpr ?_
    exact IsLocalization.integralClosure ..
  refine bijective_of_isLocalized_span s hs (F := (toIntegralClosure R S B).toLinearMap)
    (fun r â†¦ (Sáµ£ r) âŠ—[R] integralClosure R B)
    (fun r â†¦ (Algebra.TensorProduct.map (Algebra.ofId _ _) (.id _ _)).toLinearMap)
    (fun r â†¦ integralClosure (Sáµ£ r) ((Sáµ£ r) âŠ—[R] B))
    (fun r â†¦ (Ï† r).toLinearMap) fun r â†¦ ?_
  convert show Function.Bijective ((toIntegralClosure R (Sáµ£ r) B).toLinearMap.restrictScalars S)
    from H r using 1
  congr!
  refine IsLocalizedModule.ext (.powers r.1) (Algebra.TensorProduct.map (Algebra.ofId S (Sáµ£ r))
    (AlgHom.id R (integralClosure R B))).toLinearMap
    (IsLocalizedModule.map_units (S := .powers r.1) (Ï† r).toLinearMap) ?_
  ext x
  exact congr($(IsLocalizedModule.map_apply (.powers r.1)
      ((Algebra.TensorProduct.map (Algebra.ofId S (Sáµ£ r))
        (AlgHom.id R (integralClosure R B))).toLinearMap)
      (Ï† r).toLinearMap (toIntegralClosure R S B).toLinearMap (1 âŠ—â‚œ x)).1)

lemma MvPolynomial.pderiv_sumToIter {Ïƒ Î¹} (p i) :
    (sumToIter R Ïƒ Î¹ p).pderiv i = sumToIter R Ïƒ Î¹ (p.pderiv (.inl i)) := by
  classical
  induction p using MvPolynomial.induction_on with
  | C a => simp
  | add p q _ _ => simp_all
  | mul_X p n _ => cases n <;> simp_all [pderiv_X, Pi.single_apply, apply_ite]

@[simp]
lemma MvPolynomial.iterToSum_sumToIter {Ïƒ Î¹} (p) :
    iterToSum R Ïƒ Î¹ (sumToIter R Ïƒ Î¹ p) = p := (MvPolynomial.sumRingEquiv _ _ _).symm_apply_apply _

@[simp]
lemma MvPolynomial.sumToIter_iterToSum {Ïƒ Î¹} (p) :
    sumToIter R Ïƒ Î¹ (iterToSum R Ïƒ Î¹ p) = p := (MvPolynomial.sumRingEquiv _ _ _).apply_symm_apply _

theorem RingHom.IsStandardSmoothOfRelativeDimension.exists_etale_mvPolynomial
    {R S : Type*} [CommRing R] [CommRing S]
    (f : R â†’+* S) {n : â„•} (hf : f.IsStandardSmoothOfRelativeDimension n) :
    âˆƒ g : MvPolynomial (Fin n) R â†’+* S, g.comp MvPolynomial.C = f âˆ§ g.Etale := by
  classical
  let := Fintype.ofFinite
  obtain âŸ¨Î¹, Ïƒ, _, _, P, eâŸ© := hf
  let := f.toAlgebra
  let eâ‚€ : Ïƒ âŠ• Fin n â‰ƒ Î¹ := ((Equiv.ofInjective _ P.map_inj).sumCongr
      (Finite.equivFinOfCardEq (by rw [Nat.card_coe_set_eq, Set.ncard_compl,
        Set.ncard_range_of_injective P.map_inj, â† e, Algebra.Presentation.dimension])).symm).trans
      (Equiv.Set.sumCompl _)
  let e : MvPolynomial Ïƒ (MvPolynomial (Fin n) R) â‰ƒâ‚[R] P.Ring :=
    (MvPolynomial.sumAlgEquiv R _ _).symm.trans (MvPolynomial.renameEquiv _ eâ‚€)
  let Ï† := e.toAlgHom.comp (IsScalarTower.toAlgHom _ (MvPolynomial (Fin n) R) _)
  algebraize [Ï†.toRingHom, (algebraMap P.Ring S).comp Ï†.toRingHom]
  have := IsScalarTower.of_algebraMap_eq' Ï†.comp_algebraMap.symm
  have : IsScalarTower R (MvPolynomial (Fin n) R) S := .toâ‚â‚‚â‚„ _ _ P.Ring _
  refine âŸ¨algebraMap _ _, (IsScalarTower.algebraMap_eq ..).symm, ?_âŸ©
  have H : (MvPolynomial.aeval fun x â†¦ (algebraMap P.Ring S) (e (MvPolynomial.X x))).toRingHom =
      (algebraMap P.Ring S).comp e.toRingHom := by
    ext
    Â· simp [e, IsScalarTower.algebraMap_eq R (MvPolynomial (Fin n) R) S]
    Â· simp [e, @RingHom.algebraMap_toAlgebra (MvPolynomial (Fin n) R) S, Ï†]
    Â· simp [e]
  let P' : Algebra.PreSubmersivePresentation (MvPolynomial (Fin n) R) S Ïƒ Ïƒ :=
  { toGenerators := .ofSurjective (algebraMap _ _ <| e <| .X Â·) <| by
      convert P.algebraMap_surjective.comp e.surjective
      exact congr($H)
    relation := e.symm âˆ˜ P.relation
    span_range_relation_eq_ker := by
      rw [Set.range_comp, â† AlgEquiv.coe_ringEquiv e.symm, AlgEquiv.symm_toRingEquiv,
        â† Ideal.map_span, P.span_range_relation_eq_ker, Ideal.map_symm]
      exact congr(RingHom.ker $H).symm
    map := _
    map_inj := Function.injective_id }
  let P' : Algebra.SubmersivePresentation (MvPolynomial (Fin n) R) S Ïƒ Ïƒ :=
  { __ := P'
    jacobian_isUnit := by
      convert P.jacobian_isUnit using 1
      simp_rw [Algebra.PreSubmersivePresentation.jacobian_eq_jacobiMatrix_det, map_det]
      congr 1
      ext i j
      trans algebraMap P.Ring S (e ((e.symm (P.relation j)).pderiv i))
      Â· simpa [Algebra.PreSubmersivePresentation.jacobiMatrix_apply, P',
          Algebra.Generators.ofSurjective] using congr($H _)
      suffices e ((e.symm (P.relation j)).pderiv i) = (P.relation j).pderiv (P.map i) by
        simp [Algebra.PreSubmersivePresentation.jacobiMatrix_apply, this]
      simp [e, MvPolynomial.pderiv_sumToIter, â† MvPolynomial.pderiv_rename eâ‚€.injective,
        show eâ‚€ (Sum.inl i) = P.map i from rfl] }
  exact etale_algebraMap.mpr (Algebra.Etale.iff_isStandardSmoothOfRelativeDimension_zero.mpr
    âŸ¨_, _, _, inferInstance, P', by simp [Algebra.Presentation.dimension]âŸ©)

theorem RingHom.IsStandardSmooth.exists_etale_mvPolynomial
    {R S : Type*} [CommRing R] [CommRing S]
    (f : R â†’+* S) (hf : f.IsStandardSmooth) :
    âˆƒ n, âˆƒ g : MvPolynomial (Fin n) R â†’+* S, g.comp MvPolynomial.C = f âˆ§ g.Etale := by
  obtain âŸ¨_, _, _, _, âŸ¨PâŸ©âŸ© := hf
  let := f.toAlgebra
  exact âŸ¨_, RingHom.IsStandardSmoothOfRelativeDimension.exists_etale_mvPolynomial _
    âŸ¨_, _, _, â€¹_â€º, P, rflâŸ©âŸ©

instance {M : Submonoid S} [Algebra.FormallyEtale R S] : Algebra.FormallyEtale R (Localization M) :=
  have : Algebra.FormallyEtale S (Localization M) := .of_isLocalization M
  .comp _ S _

/-- Given `S` a finitely presented `R`-algebra, and `p` a prime of `S`. If `S` is smooth over `R`
at `p`, then there exists `f âˆ‰ p` such that `R â†’ S[1/f]` factors through some `R[Xâ‚,...,Xâ‚™]`,
and that `S[1/f]` is standard etale over `R[Xâ‚,...,Xâ‚™]`. -/
theorem Algebra.IsSmoothAt.exists_isStandardEtale_mvPolynomial
    {p : Ideal S} [p.IsPrime] [Algebra.FinitePresentation R S]
    [Algebra.IsSmoothAt R p] :
    âˆƒ f âˆ‰ p, âˆƒ (n : â„•) (_ : Algebra (MvPolynomial (Fin n) R) (Localization.Away f)),
      IsScalarTower R (MvPolynomial (Fin n) R) (Localization.Away f) âˆ§
      Algebra.IsStandardEtale (MvPolynomial (Fin n) R) (Localization.Away f) := by
  classical
  obtain âŸ¨f, hfp, HâŸ© := Algebra.IsSmoothAt.exists_notMem_isStandardSmooth R p
  obtain âŸ¨n, Ï†, hgC, hgâŸ© := RingHom.IsStandardSmooth.exists_etale_mvPolynomial
    (algebraMap R (Localization.Away f))
    (by delta RingHom.IsStandardSmooth; convert H; apply Algebra.algebra_ext; exact fun _ â†¦ rfl)
  algebraize [Ï†]
  have := IsScalarTower.of_algebraMap_eq' hgC.symm
  have : (Ideal.map (algebraMap S (Localization.Away f)) p).IsPrime :=
    IsLocalization.isPrime_of_isPrime_disjoint (.powers f) _ _ â€¹_â€º
      ((Ideal.disjoint_powers_iff_notMem _ (Ideal.IsPrime.isRadical â€¹_â€º)).mpr hfp)
  obtain âŸ¨gâ‚€, hg, HâŸ© := Algebra.IsEtaleAt.exists_isStandardEtale (R := (MvPolynomial (Fin n) R))
    (S := (Localization.Away f)) (p.map (algebraMap _ _))
  obtain âŸ¨g, âŸ¨_, m, rflâŸ©, hgâ‚€âŸ© := IsLocalization.exists_mk'_eq (.powers f) gâ‚€
  replace hg : g âˆ‰ p := by simpa [Submonoid.mem_powers_iff, Ideal.IsPrime.mul_mem_iff_mem_or_mem,
    IsLocalization.mk'_mem_map_algebraMap_iff, mt (â€¹p.IsPrimeâ€º.mem_of_pow_mem _) hfp,
    â† hgâ‚€] using hg
  have : IsLocalization.Away (f * g) (Localization.Away gâ‚€) := by
    suffices IsLocalization.Away (algebraMap _ (Localization.Away f) g) (Localization.Away gâ‚€) from
      .mul' (Localization.Away f) _ _ _
    refine IsLocalization.Away.of_associated (r := gâ‚€)
      âŸ¨(IsLocalization.Away.algebraMap_pow_isUnit f m).unit, ?_âŸ©
    simp only [â† hgâ‚€, IsUnit.unit_spec, â† map_pow, mul_comm, IsLocalization.mk'_spec'_mk]
  let e : Localization.Away gâ‚€ â‰ƒâ‚[S] Localization.Away (f * g) :=
    IsLocalization.algEquiv (.powers (f * g)) _ _
  let : Algebra (MvPolynomial (Fin n) R) (Localization.Away (f * g)) :=
    (e.toRingHom.comp (algebraMap (MvPolynomial (Fin n) R) _)).toAlgebra
  have : IsScalarTower R (MvPolynomial (Fin n) R) (Localization.Away (f * g)) := by
    refine .of_algebraMap_eq' ?_
    simp only [RingHom.algebraMap_toAlgebra, RingHom.comp_assoc, â† IsScalarTower.algebraMap_eq]
    exact (e.toAlgHom.comp_algebraMap_of_tower (R := R)).symm
  let e' : Localization.Away gâ‚€ â‰ƒâ‚[MvPolynomial (Fin n) R] Localization.Away (f * g) :=
    { __ := e, commutes' r := rfl }
  refine âŸ¨f * g, â€¹p.IsPrimeâ€º.mul_notMem â€¹_â€º â€¹_â€º, n, â€¹_â€º, â€¹_â€º, .of_equiv e'âŸ©

lemma fg_subgroup_pi_z {M : Type*} [Finite M] (H : AddSubgroup (M â†’ â„¤)) : H.FG :=
  (H.toIntSubmodule.fg_iff_addSubgroup_fg).mp (IsNoetherian.noetherian _)

example {K L : Type*} [Field K] [Ring L] [Algebra K L] [Nontrivial L]
    (h : Module.finrank K L = 1) : Function.Bijective (algebraMap K L) :=
  bijective_algebraMap_of_linearEquiv (Module.nonempty_linearEquiv_of_finrank_eq_one h).some

lemma TensorProduct.toIntegralClosure_injective_of_flat [Module.Flat R S] :
    Function.Injective (toIntegralClosure R S B) := by
  refine Function.Injective.of_comp (f := (integralClosure _ _).val) ?_
  rw [â† AlgHom.coe_comp, toIntegralClosure, AlgHom.val_comp_codRestrict]
  exact Module.Flat.lTensor_preserves_injective_linearMap (M := S)
    (integralClosure R B).val.toLinearMap Subtype.val_injective

lemma RingHom.IsIntegralElem.of_comp_of_injective
    {R S T : Type*} [CommRing R] [CommRing S] [CommRing T]
    {f : R â†’+* S} {g : S â†’+* T} {x : S} (hg : Function.Injective g)
    (hx : (g.comp f).IsIntegralElem (g x)) :
    f.IsIntegralElem x := by
  obtain âŸ¨p, hp, hxâŸ© := hx
  exact âŸ¨p, hp, hg <| by simp [hom_evalâ‚‚, hx]âŸ©

lemma MvPolynomial.killCompl_map
    {Ïƒ Ï„ R S : Type*} [CommSemiring R] [CommSemiring S] {f : Ïƒ â†’ Ï„}
    (hf : f.Injective) (Ï† : R â†’+* S) (p : MvPolynomial _ R) :
    (p.map Ï†).killCompl hf = (p.killCompl hf).map Ï† := by
  simp only [â† AlgHom.coe_toRingHom, â† RingHom.comp_apply]
  congr
  ext i n
  Â· simp
  Â· by_cases h : i âˆˆ Set.range f <;> simp [MvPolynomial.killCompl, h]

@[simp]
lemma MvPolynomial.optionEquivLeft_symm_C_C (R Sâ‚ : Type*) [CommSemiring R] (x : R) :
    (optionEquivLeft R Sâ‚).symm (.C (.C x)) = .C x := by simp [optionEquivLeft]

@[simp]
lemma MvPolynomial.optionEquivLeft_symm_X (R Sâ‚ : Type*) [CommSemiring R] :
    (optionEquivLeft R Sâ‚).symm .X = .X .none := by simp [optionEquivLeft]

@[elab_as_elim]
lemma Finite.induction_empty_option'.{u} {P : âˆ€ (Î± : Type u) [Finite Î±], Prop}
    (of_equiv : âˆ€ {Î± Î² : Type u} (_ : Î± â‰ƒ Î²) [Finite Î±] [Finite Î²], P Î± â†’ P Î²)
    (h_empty : P PEmpty.{u + 1}) (h_option : âˆ€ {Î± : Type u} [Fintype Î±],
    P Î± â†’ P (Option Î±)) (Î± : Type u) (hÎ± : Finite Î±) : P Î± := by
  refine Finite.induction_empty_option (P := fun Î± â†¦ (h : Finite Î±) â†’ P Î±) ?_ ?_ ?_ Î± â€¹Finite Î±â€º
  Â· exact fun Î± Î² e IH {_} â†¦ have := Finite.of_equiv _ e.symm; of_equiv e (IH _)
  Â· exact fun _ â†¦ h_empty
  Â· exact fun Î± _ IH {_} â†¦ h_option (IH _)

universe w in
attribute [local instance] MvPolynomial.algebraMvPolynomial in
attribute [-simp] AlgEquiv.symm_toRingEquiv in
attribute [simp] MvPolynomial.optionEquivLeft_C MvPolynomial.optionEquivLeft_X_none
  MvPolynomial.optionEquivLeft_X_some in
theorem MvPolynomial.isIntegral_iff_isIntegral_coeff {Ïƒ : Type w} {f : MvPolynomial Ïƒ S} :
    IsIntegral (MvPolynomial Ïƒ R) f â†” âˆ€ n, IsIntegral R (f.coeff n) := by
  classical
  refine âŸ¨fun H n â†¦ ?mp, fun H â†¦ ?mprâŸ©
  case mpr =>
    rw [â† f.support_sum_monomial_coeff]
    simp_rw [monomial_eq]
    refine IsIntegral.sum _ fun n _ â†¦ .mul ((H n).map (Algebra.ofId _ _)).tower_top
      (.prod _ fun i _ â†¦ .pow ?_ _)
    convert isIntegral_algebraMap (x := MvPolynomial.X i)
    simp only [algebraMap_def, map_X]
  unfold IsIntegral at H
  wlog hÏƒ : Finite Ïƒ generalizing Ïƒ
  Â· obtain âŸ¨g, hgâŸ© := MvPolynomial.exists_rename_eq_of_vars_subset_range (Ï„ := f.vars) f _
      Subtype.val_injective (by simp)
    by_cases hn : n âˆˆ Set.range (Finsupp.mapDomain ((â†‘) : f.vars â†’ Ïƒ))
    Â· obtain âŸ¨n, rflâŸ© := hn
      simp_rw [â† hg, coeff_rename_mapDomain _ Subtype.val_injective]
      exact this (f := g) (RingHom.IsIntegralElem.of_comp_of_injective
        (g := (rename ((â†‘) : f.vars â†’ Ïƒ)).toRingHom) (rename_injective _ Subtype.val_injective)
        (.of_comp (f := (killCompl (f := ((â†‘) : f.vars â†’ Ïƒ)) Subtype.val_injective).toRingHom) <| by
        simp only [AlgHom.toRingHom_eq_coe, algebraMap_def, RingHom.coe_coe, hg]
        convert H.map ((rename Subtype.val).comp
          (killCompl (f := ((â†‘) : f.vars â†’ Ïƒ)) Subtype.val_injective)).toRingHom
        Â· exact RingHom.ext (by simp [MvPolynomial.killCompl_map])
        Â· nth_rw 1 11 [â† hg]; simp)) n (.of_fintype _)
    Â· rw [â† hg, coeff_rename_eq_zero _ _ _ (by grind)]
      exact isIntegral_zero
  induction Ïƒ, hÏƒ using Finite.induction_empty_option' with
  | @of_equiv Î± Î² e _ _ IH =>
    have := @IH (rename e.symm f) (.of_comp_of_injective (g := (rename e).toRingHom)
      (rename_injective _ e.injective) <| .of_comp (f := (rename e.symm).toRingHom)
        (by convert H <;> aesop)) (n.embDomain e.symm)
    simpa [Finsupp.embDomain_eq_mapDomain, coeff_rename_mapDomain _ e.symm.injective] using this
  | h_empty =>
    refine .of_comp_of_injective (g := (isEmptyAlgEquiv _ PEmpty).symm.toRingHom)
      (isEmptyAlgEquiv _ PEmpty).symm.injective
      (.of_comp (f := (isEmptyAlgEquiv _ PEmpty).toRingHom) ?_)
    convert H
    Â· aesop (add simp MvPolynomial.isEmptyAlgEquiv)
    Â· obtain rfl := Subsingleton.elim n 0
      have : constantCoeff = (isEmptyAlgEquiv S PEmpty).toRingHom := by aesop
      simpa [-EmbeddingLike.apply_eq_iff_eq, -isEmptyAlgEquiv_apply] using
        congr((isEmptyAlgEquiv S PEmpty.{w + 1}).symm ($this f))
  | @h_option Î± hÎ± IH =>
    have := IH (_root_.isIntegral_coeff_of_isIntegral (R := MvPolynomial Î± R)
      (f := optionEquivLeft _ _ f) (.of_comp_of_injective
      (g := (optionEquivLeft _ _).symm.toRingHom) (optionEquivLeft _ _).symm.injective
      (.of_comp (f := (optionEquivLeft _ _).toRingHom) (by
        convert H
        Â· ext i m
          Â· aesop
          Â· cases i <;> aesop
        Â· aesop))) (n .none)) n.some
    rwa [optionEquivLeft_coeff_some_coeff_none] at this

attribute [local instance] MvPolynomial.algebraMvPolynomial in
lemma TensorProduct.toIntegralClosure_mvPolynomial_bijective {Ïƒ : Type*} :
    Function.Bijective (toIntegralClosure R (MvPolynomial Ïƒ R) B) := by
  classical
  refine âŸ¨toIntegralClosure_injective_of_flat, ?_âŸ©
  rintro âŸ¨x, hxâŸ©
  let e : MvPolynomial Ïƒ R âŠ—[R] B â‰ƒâ‚[MvPolynomial Ïƒ R] MvPolynomial Ïƒ B :=
    { toRingEquiv := MvPolynomial.scalarRTensorAlgEquiv.toRingEquiv, commutes' r := by
        change MvPolynomial.scalarRTensorAlgEquiv.toRingHom.comp (algebraMap _ _) r = _
        congr 1
        ext <;> simp [MvPolynomial.scalarRTensorAlgEquiv, MvPolynomial.coeff_map,
          â† Algebra.algebraMap_eq_smul_one, apply_ite (algebraMap _ _), MvPolynomial.coeff_X'] }
  have := MvPolynomial.isIntegral_iff_isIntegral_coeff.mp (hx.map e)
  obtain âŸ¨y, hyâŸ© : e x âˆˆ RingHom.range (MvPolynomial.map (integralClosure R B).val.toRingHom) := by
    refine MvPolynomial.mem_range_map_iff_coeffs_subset.mpr ?_
    simp [Set.subset_def, mem_integralClosure_iff, MvPolynomial.mem_coeffs_iff,
      @forall_comm B, this]
  refine âŸ¨MvPolynomial.scalarRTensorAlgEquiv.symm y, Subtype.ext <| e.injective (.trans ?_ hy)âŸ©
  obtain âŸ¨y, rflâŸ© := (MvPolynomial.scalarRTensorAlgEquiv (R := R)).surjective y
  dsimp [TensorProduct.toIntegralClosure, e]
  simp only [AlgEquiv.symm_apply_apply]
  have : MvPolynomial.scalarRTensorAlgEquiv.toAlgHom.comp
      (Algebra.TensorProduct.map (AlgHom.id R (MvPolynomial Ïƒ R)) (integralClosure R B).val) =
      (MvPolynomial.mapAlgHom (integralClosure R B).val).comp
      MvPolynomial.scalarRTensorAlgEquiv.toAlgHom := by
    ext <;> simp [-MvPolynomial.mapAlgHom_apply, MvPolynomial.mapAlgHom, MvPolynomial.coeff_map,
      MvPolynomial.scalarRTensorAlgEquiv]
  exact congr($this y)

lemma TensorProduct.toIntegralClosure_bijective_of_tower
    {T : Type*} [CommRing T] [Algebra R T] [Algebra S T] [IsScalarTower R S T]
    (H : Function.Bijective (toIntegralClosure R S B))
    (H' : Function.Bijective (toIntegralClosure S T (S âŠ—[R] B))) :
    Function.Bijective (toIntegralClosure R T B) := by
  let e := (Algebra.TensorProduct.cancelBaseChange ..).symm.trans <|
      (Algebra.TensorProduct.congr (.refl (R := T) (Aâ‚ := T)) (.ofBijective _ H)).trans <|
      (AlgEquiv.ofBijective _ H').trans <|
      (AlgEquiv.mapIntegralClosure (Algebra.TensorProduct.cancelBaseChange ..))
  convert e.bijective
  rw [â† e.coe_algHom]
  congr 1
  ext; simp [e, toIntegralClosure]

theorem TensorProduct.toIntegralClosure_bijective_of_isStandardEtale
    {B : Type*} [CommRing B] [Algebra R B] [Algebra.IsStandardEtale R S] :
    Function.Bijective (toIntegralClosure R S B) := by
  have : Algebra.Smooth R S := {}
  refine âŸ¨toIntegralClosure_injective_of_flat, ?_âŸ©
  intro âŸ¨x, hxâŸ©
  simp only [toIntegralClosure, Subtype.ext_iff, AlgHom.coe_codRestrict, â† AlgHom.mem_range]
  refine Algebra.adjoin_le ?_ (mem_adjoin_map_integralClosure_of_isStandardEtale x hx)
  rintro _ âŸ¨y, hy : IsIntegral _ _, rflâŸ©
  refine âŸ¨1 âŠ—â‚œ âŸ¨y, hyâŸ©, by simpâŸ©

theorem TensorProduct.toIntegralClosure_bijective_of_smooth
    {B : Type*} [CommRing B] [Algebra R B] [Algebra.Smooth R S] :
    Function.Bijective (toIntegralClosure R S B) := by
  have (m : PrimeSpectrum S) : âˆƒ f âˆ‰ m.asIdeal,
      Function.Bijective (toIntegralClosure R (Localization.Away f) B) := by
    obtain âŸ¨f, hfm, n, _, _, _âŸ© :=
      Algebra.IsSmoothAt.exists_isStandardEtale_mvPolynomial (R := R) (p := m.asIdeal)
    exact âŸ¨f, hfm, toIntegralClosure_bijective_of_tower (S := MvPolynomial (Fin n) R)
      toIntegralClosure_mvPolynomial_bijective toIntegralClosure_bijective_of_isStandardEtaleâŸ©
  choose f hfm hf using this
  refine TensorProduct.toIntegralClosure_bijective_of_isLocalizationAway (R := R)
    (s := Set.range f) (B := B) ?_ (Localization.Away Â·.1) (Set.forall_subtype_range_iff.mpr hf)
  by_contra H
  obtain âŸ¨m, hm, eâŸ© := Ideal.exists_le_maximal _ H
  exact hfm âŸ¨m, inferInstanceâŸ© (e (Ideal.subset_span (Set.mem_range_self âŸ¨m, inferInstanceâŸ©)):)
