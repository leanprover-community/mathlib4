
import Mathlib.AlgebraicGeometry.Normalization
import Mathlib.CFT.Flat
import Mathlib.CFT.IntegralClosure
import Mathlib.CFT.Smooth

open CategoryTheory Limits

namespace AlgebraicGeometry

universe u

variable {X S Y : Scheme.{u}} (f : X âŸ¶ S) (g : Y âŸ¶ S) [QuasiCompact f] [QuasiSeparated f]

lemma isIso_morphismRestrict_iff_isIso_app
    (f : X âŸ¶ Y) [IsAffineHom f] {U : Y.Opens} (hU : IsAffineOpen U) :
    IsIso (f âˆ£_ U) â†” IsIso (f.app U) := by
  have : IsAffine U := hU
  refine (HasAffineProperty.iff_of_isAffine (P := .isomorphisms _)).trans <|
    (and_iff_right (hU.preimage f)).trans ?_
  rw [Scheme.Hom.app_eq_appLE]
  simp only [morphismRestrict_app', TopologicalSpace.Opens.map_top]
  congr! <;> simp [Scheme.Opens.toScheme_presheaf_obj]

lemma isIso_pullbackSnd_opensÎ¹_iff_isIso_app
    (f : X âŸ¶ Y) [IsAffineHom f] {U : Y.Opens} (hU : IsAffineOpen U) :
    IsIso (pullback.snd f U.Î¹) â†” IsIso (f.app U) := by
  have : IsAffine U := hU
  rw [â† pullbackRestrictIsoRestrict_hom_morphismRestrict, isIso_comp_left_iff,
    isIso_morphismRestrict_iff_isIso_app f hU]

lemma IsZariskiLocalAtTarget.of_forall_exists_isOpen {P : MorphismProperty Scheme}
    [IsZariskiLocalAtTarget P] {X Y : Scheme} {f : X âŸ¶ Y}
    (H : âˆ€ x, âˆƒ U : Y.Opens, x âˆˆ U âˆ§ P (f âˆ£_ U)) : P f := by
  choose U hxU hU using H
  refine IsZariskiLocalAtTarget.of_iSup_eq_top U (top_le_iff.mp fun x _ â†¦ ?_) hU
  simpa using âŸ¨x, hxU xâŸ©

lemma IsIso_of_isAffineHom_of_forall_isAffineOpen {X S Y : Scheme.{u}} (fY : Y âŸ¶ S)
    [IsAffineHom fY] (f : X âŸ¶ Y) [IsAffineHom f]
    (H : âˆ€ U : S.Opens, IsAffineOpen U â†’ IsIso (f.app (fY â»Â¹áµ U))) : IsIso f := by
  refine (IsZariskiLocalAtTarget.iff_of_openCover (P := .isomorphisms _) (Y.openCoverOfIsOpenCover _
    (.comap (iSup_affineOpens_eq_top S) fY.base.hom))).mpr fun (U : S.affineOpens) â†¦ ?_
  have : IsAffine (fY â»Â¹áµ â†‘U) := U.2.preimage fY
  refine (HasAffineProperty.iff_of_isAffine (P := .isomorphisms _)
    (f := pullback.snd f (fY â»Â¹áµ U).Î¹)).mpr âŸ¨isAffine_of_isAffineHom (pullback.snd _ _), ?_âŸ©
  rw [â† pullbackRestrictIsoRestrict_hom_morphismRestrict, Scheme.Hom.comp_appTop]
  simp_rw [Scheme.Hom.app_eq_appLE] at H
  simp only [morphismRestrict_app', TopologicalSpace.Opens.map_top, isIso_comp_right_iff]
  suffices âˆ€ Vâ‚ Vâ‚‚ (hâ‚ : Vâ‚ = fY â»Â¹áµ â†‘U) (hâ‚‚ : Vâ‚‚ = f â»Â¹áµ fY â»Â¹áµ â†‘U),
      IsIso (f.appLE Vâ‚ Vâ‚‚ (by simp_all)) from this _ _ (by simp) (by simp)
  exact fun _ _ eâ‚ eâ‚‚ â†¦ by subst eâ‚ eâ‚‚; exact H U.1 U.2

noncomputable
def Scheme.Hom.normalizationPullback :
    (pullback.snd f g).normalization âŸ¶ pullback f.fromNormalization g :=
  (pullback.snd f g).normalizationDesc (pullback.map _ _ _ _ f.toNormalization
    (ğŸ™ _) (ğŸ™ _) (by simp) (by simp)) (pullback.snd _ _) (by simp)
  deriving IsIntegralHom

@[reassoc (attr := simp)]
lemma Scheme.Hom.normalizationPullback_snd :
    f.normalizationPullback g â‰« pullback.snd _ _ = (pullback.snd f g).fromNormalization :=
  (pullback.snd f g).normalizationDesc_comp ..

@[reassoc (attr := simp)]
lemma Scheme.Hom.toNormalization_normalizationPullback_fst :
    (pullback.snd f g).toNormalization â‰« f.normalizationPullback g â‰« pullback.fst _ _ =
      pullback.fst _ _ â‰« f.toNormalization := by
  simp [normalizationPullback]

noncomputable
def Scheme.Hom.normalizationObjIso (f : X âŸ¶ Y) [QuasiCompact f] [QuasiSeparated f]
    {U : Y.Opens} (hU : IsAffineOpen U) :
    letI := (f.app U).hom.toAlgebra
    Î“(f.normalization, f.fromNormalization â»Â¹áµ U) â‰…
      .of (integralClosure Î“(Y, U) Î“(X, f â»Â¹áµ U)) :=
  f.normalization.presheaf.mapIso (eqToIso
    (by simpa using (f.fromNormalization_preimage âŸ¨U, hUâŸ©).symm)).op â‰ªâ‰«
  (f.normalizationOpenCover.f âŸ¨U, hUâŸ©).appIso âŠ¤ â‰ªâ‰« Scheme.Î“SpecIso _

lemma Scheme.Hom.fromNormalization_app (f : X âŸ¶ Y) [QuasiCompact f] [QuasiSeparated f]
    {U : Y.Opens} (hU : IsAffineOpen U) :
    f.fromNormalization.app U = CommRingCat.ofHom (algebraMap _ _) â‰«
      (f.normalizationObjIso hU).inv := by
  letI := (f.app U).hom.toAlgebra
  have : IsIso (((normalizationOpenCover f).f âŸ¨U, hUâŸ©).app (f.fromNormalization â»Â¹áµ U)) :=
    Scheme.Hom.isIso_app _ _ (by simp [â† fromNormalization_preimage])
  have H : âŠ¤ = ((normalizationOpenCover f).f âŸ¨U, hUâŸ© â‰« fromNormalization f) â»Â¹áµ U := by
    rw [f.Î¹_fromNormalization]; simp
  rw [â† cancel_mono (((normalizationOpenCover f).f âŸ¨U, hUâŸ©).app (f.fromNormalization â»Â¹áµ U)),
    â† Scheme.Hom.comp_app, Scheme.Hom.congr_app (f.Î¹_fromNormalization âŸ¨U, hUâŸ©) U,
    â† cancel_mono (((normalizationOpenCover f).X âŸ¨U, hUâŸ©).presheaf.map (eqToHom H).op)]
  dsimp [normalizationObjIso]
  rw [IsAffineOpen.fromSpec_app_self]
  simp only [app_eq_appLE, Category.assoc, map_appLE, appLE_map, appIso_inv_appLE]
  simp [Scheme.Hom.appLE, â† Î“SpecIso_inv_naturality]
  rfl

lemma Scheme.Hom.normalizationObjIso_hom_val (f : X âŸ¶ Y) [QuasiCompact f] [QuasiSeparated f]
    {U : Y.Opens} (hU : IsAffineOpen U) :
    letI := (f.app U).hom.toAlgebra
    (f.normalizationObjIso hU).hom â‰« CommRingCat.ofHom (Subalgebra.val _).toRingHom =
    f.toNormalization.appLE _ _ (by simp [â† Scheme.Hom.comp_preimage]) := by
  dsimp [Scheme.Hom.normalizationObjIso]
  rw [Category.assoc, Category.assoc, â† IsIso.eq_inv_comp, â† Functor.map_inv, map_appLE]
  have H : toNormalization f â»Â¹áµ (normalizationOpenCover f).f âŸ¨U, hUâŸ© ''áµ âŠ¤ = f â»Â¹áµ U := by
    simp [â† fromNormalization_preimage, â† Scheme.Hom.comp_preimage]
  have : IsIso ((f â»Â¹áµ U).Î¹.app
      (toNormalization f â»Â¹áµ (normalizationOpenCover f).f âŸ¨U, hUâŸ© ''áµ âŠ¤)) :=
    isIso_app _ _ (by rw [H]; simp)
  rw [â† cancel_mono (X.presheaf.map (eqToHom H).op), â† cancel_mono ((f â»Â¹áµ U).Î¹.app _)]
  simp only [appLE_map, app_eq_appLE, appLE_comp_appLE]
  rw [Opens.Î¹_appLE, appLE, congr_app (f.Î¹_toNormalization âŸ¨U, hUâŸ©)]
  dsimp [Opens.toScheme_presheaf_obj]
  simp only [Category.assoc, â† Functor.map_comp, â† Iso.eq_inv_comp, appIso_inv_app_assoc,
    naturality_assoc, TopologicalSpace.Opens.map_top, Opens.toSpecÎ“_appTop, Opens.topIso_inv,
    Quiver.Hom.unop_op, Opens.toScheme_presheaf_map]
  rw [Î“SpecIso_naturality_assoc]
  exact ((Î“SpecIso ((normalizationDiagram f).obj _)).inv_hom_id_assoc _).symm

open TensorProduct in
set_option maxHeartbeats 0 in
instance [Smooth g] : IsIso (f.normalizationPullback g) := by
  apply IsZariskiLocalAtTarget.of_forall_exists_isOpen (P := .isomorphisms _) fun x â†¦ ?_
  obtain âŸ¨_, âŸ¨U, hU, rflâŸ©, hxU, -âŸ© := S.isBasis_affineOpens.exists_subset_of_mem_open
    (Set.mem_univ ((pullback.snd _ g â‰« g) x)) isOpen_univ
  obtain âŸ¨_, âŸ¨V, hV, rflâŸ©, hxV, hVU : V â‰¤ g â»Â¹áµ UâŸ© :=
    Y.isBasis_affineOpens.exists_subset_of_mem_open (a := pullback.snd _ g x) hxU (g â»Â¹áµ U).2
  let W := pullback.snd (Scheme.Hom.fromNormalization f) g â»Â¹áµ V
  refine âŸ¨W, hxV, (isIso_morphismRestrict_iff_isIso_app _ (U := W) (hV.preimage _)).mpr ?_âŸ©
  have := isIso_pushoutDesc_appLE_appLE_of_isCompact_of_isQuasiSeparated_of_flat
    (.of_hasPullback f.fromNormalization g) hU hV hVU le_rfl (UY := W)
    (by simp_rw [W, â† Scheme.Hom.comp_preimage, pullback.condition, Scheme.Hom.comp_preimage,
      â† Scheme.Hom.preimage_inf, inf_eq_right.mpr hVU])
    (hU.preimage f.fromNormalization).isCompact (hU.preimage f.fromNormalization).isQuasiSeparated
  rw [â† @isIso_comp_left_iff _ _ _ _ _ _ _ this,
    â† isIso_comp_left_iff (pushout.congrHom f.fromNormalization.app_eq_appLE rfl).hom]
  have : (g.appLE U V hVU).hom.Smooth := Smooth.smooth_of_affine_subset âŸ¨U, hUâŸ© âŸ¨V, hVâŸ© _
  algebraize [(f.app U).hom, (g.appLE U V hVU).hom, ((pullback.snd f g).app V).hom]
  have := isIso_pushoutDesc_appLE_appLE_of_isCompact_of_isQuasiSeparated_of_flat
    (.of_hasPullback f g) hU hV hVU le_rfl (UY := pullback.snd f g â»Â¹áµ V)
    (by simp_rw [â† Scheme.Hom.comp_preimage, pullback.condition, Scheme.Hom.comp_preimage,
      â† Scheme.Hom.preimage_inf, inf_eq_right.mpr hVU]) (f.isCompact_preimage hU.isCompact)
    (f.isQuasiSeparated_preimage hU.isQuasiSeparated)
  let eâ‚€ := (CommRingCat.isPushout_tensorProduct ..).flip.isoPushout â‰ªâ‰«
    (pushout.congrHom f.app_eq_appLE rfl â‰ªâ‰« @asIso _ _ _ _ _ this:)
  let e : Î“(Y, V) âŠ—[Î“(S, U)] Î“(X, f â»Â¹áµ U) â‰ƒâ‚[Î“(Y, V)] Î“(pullback f g, pullback.snd f g â»Â¹áµ V) :=
    { toRingEquiv := eâ‚€.commRingCatIsoToRingEquiv,
      commutes' r := by
        change (CommRingCat.ofHom Algebra.TensorProduct.includeLeftRingHom â‰« eâ‚€.hom) r =
          (pullback.snd f g).app V r
        congr 2
        simp [eâ‚€, pushout.inr_desc_assoc, Scheme.Hom.app_eq_appLE] }
  let Ïˆ : Î“(Y, V) âŠ—[Î“(S, U)] integralClosure Î“(S, U) Î“(X, f â»Â¹áµ U) â†’â‚[Î“(Y, V)]
      integralClosure Î“(Y, V) Î“(pullback f g, pullback.snd f g â»Â¹áµ V) :=
    e.mapIntegralClosure.toAlgHom.comp (TensorProduct.toIntegralClosure _ _ _)
  have hÏˆ : Function.Bijective Ïˆ := e.mapIntegralClosure.bijective.comp
    TensorProduct.toIntegralClosure_bijective_of_smooth
  let Ï† : pushout (f.fromNormalization.app U) (g.appLE U V hVU) âŸ¶
      Î“((pullback.snd f g).normalization, f.normalizationPullback g â»Â¹áµ W) :=
    pushout.map _ _ (CommRingCat.ofHom (algebraMap Î“(S, U) (integralClosure Î“(S, U) Î“(X, f â»Â¹áµ U))))
      (g.appLE U V hVU) (f.normalizationObjIso hU).hom (ğŸ™ _) (ğŸ™ _)
      (by simp [Scheme.Hom.fromNormalization_app _ hU]) (by simp) â‰«
    (CommRingCat.isPushout_tensorProduct ..).flip.isoPushout.inv â‰«
    (RingEquiv.ofBijective Ïˆ.toRingHom hÏˆ).toCommRingCatIso.hom â‰«
    ((pullback.snd f g).normalizationObjIso hV).inv â‰«
    (pullback.snd f g).normalization.presheaf.map (eqToHom
      (by simp only [W, â† Scheme.Hom.comp_preimage, Scheme.Hom.normalizationPullback_snd])).op
  convert show IsIso Ï† by dsimp only [Ï†]; infer_instance using 1
  ext1
  Â· dsimp [Ï†]
    simp only [Scheme.Hom.app_eq_appLE, colimit.Î¹_desc_assoc, span_left, PushoutCocone.mk_pt,
      PushoutCocone.mk_Î¹_app, Category.id_comp, Scheme.Hom.appLE_comp_appLE, eqToHom_op,
      Category.assoc, IsPushout.inl_isoPushout_inv_assoc]
    simp_rw [â† Category.assoc, â† IsIso.comp_inv_eq]
    simp only [â† Functor.map_inv, inv_eqToHom, Scheme.Hom.appLE_map, IsIso.Iso.inv_inv,
      Category.assoc]
    have : Mono (CommRingCat.ofHom (integralClosure Î“(Y, V)
        Î“(pullback f g, pullback.snd f g â»Â¹áµ V)).val.toRingHom) :=
      ConcreteCategory.mono_of_injective _ Subtype.val_injective
    rw [â† cancel_mono (CommRingCat.ofHom (Subalgebra.val _).toRingHom)]
    simp only [Category.assoc, Scheme.Hom.normalizationObjIso_hom_val, Scheme.Hom.appLE_comp_appLE,
      Scheme.Hom.toNormalization_normalizationPullback_fst, â† CommRingCat.ofHom_comp]
    have H : pullback.snd f g â»Â¹áµ V â‰¤ pullback.fst f g â»Â¹áµ f â»Â¹áµ U := by
      rw [â† Scheme.Hom.comp_preimage, pullback.condition, Scheme.Hom.comp_preimage]
      exact Scheme.Hom.preimage_mono _ hVU
    trans (f.normalizationObjIso hU).hom â‰« CommRingCat.ofHom
        (integralClosure Î“(S, U) Î“(X, f â»Â¹áµ U)).val.toRingHom â‰« (pullback.fst f g).appLE _ _ H
    Â· rw [reassoc_of% Scheme.Hom.normalizationObjIso_hom_val, Scheme.Hom.appLE_comp_appLE]
    Â· congr 1
      ext x
      change (pullback.fst f g).appLE _ _ H x = _
      trans (CommRingCat.ofHom Algebra.TensorProduct.includeRight.toRingHom â‰« eâ‚€.hom) x
      Â· congr 2; simp [eâ‚€, pushout.inl_desc_assoc]
      Â· simp [Ïˆ, toIntegralClosure, e]; rfl
  Â· dsimp [Ï†]
    simp only [Scheme.Hom.app_eq_appLE, colimit.Î¹_desc_assoc, span_right, PushoutCocone.mk_pt,
      PushoutCocone.mk_Î¹_app, Category.id_comp, Scheme.Hom.appLE_comp_appLE,
      Scheme.Hom.normalizationPullback_snd, eqToHom_op, IsPushout.inr_isoPushout_inv_assoc]
    simp_rw [â† Category.assoc, â† IsIso.comp_inv_eq]
    simp only [â† Functor.map_inv, inv_eqToHom, Scheme.Hom.appLE_map, â† Scheme.Hom.app_eq_appLE,
      Scheme.Hom.fromNormalization_app _ hV, IsIso.Iso.inv_inv, Category.assoc, Iso.inv_hom_id,
      Category.comp_id]
    exact congr(CommRingCat.ofHom $(Ïˆ.comp_algebraMap.symm))

end AlgebraicGeometry
