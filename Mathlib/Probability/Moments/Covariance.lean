/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Etienne Marion
-/
import Mathlib.Probability.Independence.Integration

/-!
# Covariance

We define the covariance of two real-valued random variables.

## Main definitions

* `covariance`: covariance of two real-valued random variables, with notation `cov[X, Y; Î¼]`.
  `cov[X, Y; Î¼] = âˆ« Ï‰, (X Ï‰ - Î¼[X]) * (Y Ï‰ - Î¼[Y]) âˆ‚Î¼`.

## Main statements

* `covariance_self`: `cov[X, X; Î¼] = Var[X; Î¼]`

## Notation

* `cov[X, Y; Î¼] = covariance X Y Î¼`
* `cov[X, Y] = covariance X Y volume`

-/

open MeasureTheory

namespace ProbabilityTheory

variable {Î© : Type*} {mÎ© : MeasurableSpace Î©} {X Y Z : Î© â†’ â„} {Î¼ : Measure Î©}

/-- The covariance of two real-valued random variables defined as
the integral of `(X - ğ”¼[X])(Y - ğ”¼[Y])`. -/
noncomputable def covariance (X Y : Î© â†’ â„) (Î¼ : Measure Î©) : â„ :=
  âˆ« Ï‰, (X Ï‰ - Î¼[X]) * (Y Ï‰ - Î¼[Y]) âˆ‚Î¼

@[inherit_doc]
scoped notation "cov[" X ", " Y "; " Î¼ "]" => ProbabilityTheory.covariance X Y Î¼

/-- The covariance of the real-valued random variables `X` and `Y`
according to the volume measure. -/
scoped notation "cov[" X ", " Y "]" => cov[X, Y; MeasureTheory.MeasureSpace.volume]

lemma covariance_eq_sub [IsProbabilityMeasure Î¼] (hX : MemLp X 2 Î¼) (hY : MemLp Y 2 Î¼) :
     cov[X, Y; Î¼] = Î¼[X * Y] - Î¼[X] * Î¼[Y] := by
   simp_rw [covariance, sub_mul, mul_sub]
   repeat rw [integral_sub]
   Â· simp_rw [integral_mul_const, integral_const_mul, integral_const, measureReal_univ_eq_one,
       one_smul]
     simp
   Â· exact hY.const_mul _ |>.integrable (by simp)
   Â· exact integrable_const _
   Â· exact hX.integrable_mul hY
   Â· exact hX.mul_const _ |>.integrable (by simp)
   Â· exact (hX.integrable_mul hY).sub (hX.mul_const _ |>.integrable (by simp))
   Â· exact (hY.const_mul _ |>.integrable (by simp)).sub (integrable_const _)

@[simp] lemma covariance_zero_left : cov[0, Y; Î¼] = 0 := by simp [covariance]

@[simp] lemma covariance_zero_right : cov[X, 0; Î¼] = 0 := by simp [covariance]

@[simp] lemma covariance_zero_measure : cov[X, Y; (0 : Measure Î©)] = 0 := by simp [covariance]

variable (X Y) in
lemma covariance_comm : cov[X, Y; Î¼] = cov[Y, X; Î¼] := by
  simp_rw [covariance]
  congr with x
  ring

@[simp]
lemma covariance_const_left [IsProbabilityMeasure Î¼] (c : â„) : cov[fun _ â†¦ c, Y; Î¼] = 0 := by
  simp [covariance]

@[simp]
lemma covariance_const_right [IsProbabilityMeasure Î¼] (c : â„) : cov[X, fun _ â†¦ c; Î¼] = 0 := by
  simp [covariance]

@[simp]
lemma covariance_add_const_left [IsProbabilityMeasure Î¼] (hX : Integrable X Î¼) (c : â„) :
    cov[fun Ï‰ â†¦ X Ï‰ + c, Y; Î¼] = cov[X, Y; Î¼] := by
  simp_rw [covariance]
  congr with Ï‰
  rw [integral_add hX (by fun_prop)]
  simp

@[simp]
lemma covariance_const_add_left [IsProbabilityMeasure Î¼] (hX : Integrable X Î¼) (c : â„) :
    cov[fun Ï‰ â†¦ c + X Ï‰, Y; Î¼] = cov[X, Y; Î¼] := by
  simp_rw [add_comm c]
  exact covariance_add_const_left hX c

@[simp]
lemma covariance_add_const_right [IsProbabilityMeasure Î¼] (hY : Integrable Y Î¼) (c : â„) :
    cov[X, fun Ï‰ â†¦ Y Ï‰ + c; Î¼] = cov[X, Y; Î¼] := by
  rw [covariance_comm, covariance_add_const_left hY c, covariance_comm]

@[simp]
lemma covariance_const_add_right [IsProbabilityMeasure Î¼] (hY : Integrable Y Î¼) (c : â„) :
    cov[X, fun Ï‰ â†¦ c + Y Ï‰; Î¼] = cov[X, Y; Î¼] := by
  simp_rw [add_comm c]
  exact covariance_add_const_right hY c

lemma covariance_add_left [IsFiniteMeasure Î¼]
    (hX : MemLp X 2 Î¼) (hY : MemLp Y 2 Î¼) (hZ : MemLp Z 2 Î¼) :
    cov[X + Y, Z; Î¼] = cov[X, Z; Î¼] + cov[Y, Z; Î¼] := by
  simp_rw [covariance, Pi.add_apply]
  rw [â† integral_add]
  Â· congr with x
    rw [integral_add (hX.integrable (by simp)) (hY.integrable (by simp))]
    ring
  Â· exact (hX.sub (memLp_const _)).integrable_mul (hZ.sub (memLp_const _))
  Â· exact (hY.sub (memLp_const _)).integrable_mul (hZ.sub (memLp_const _))

lemma covariance_add_right [IsFiniteMeasure Î¼]
    (hX : MemLp X 2 Î¼) (hY : MemLp Y 2 Î¼) (hZ : MemLp Z 2 Î¼) :
    cov[X, Y + Z; Î¼] = cov[X, Y; Î¼] + cov[X, Z; Î¼] := by
  rw [covariance_comm, covariance_add_left hY hZ hX, covariance_comm X, covariance_comm Z]

lemma covariance_smul_left (c : â„) : cov[c â€¢ X, Y; Î¼] = c * cov[X, Y; Î¼] := by
  simp_rw [covariance, Pi.smul_apply, smul_eq_mul, â† integral_const_mul, â† mul_assoc, mul_sub,
    integral_const_mul]

lemma covariance_smul_right (c : â„) : cov[X, c â€¢ Y; Î¼] = c * cov[X, Y; Î¼] := by
  rw [covariance_comm, covariance_smul_left, covariance_comm]

lemma covariance_mul_left (c : â„) : cov[fun Ï‰ â†¦ c * X Ï‰, Y; Î¼] = c * cov[X, Y; Î¼] :=
  covariance_smul_left c

lemma covariance_mul_right (c : â„) : cov[X, fun Ï‰ â†¦ c * Y Ï‰; Î¼] = c * cov[X, Y; Î¼] :=
  covariance_smul_right c

@[simp]
lemma covariance_neg_left : cov[-X, Y; Î¼] = -cov[X, Y; Î¼] := by
  calc cov[-X, Y; Î¼]
  _ = cov[(-1 : â„) â€¢ X, Y; Î¼] := by simp
  _ = - cov[X, Y; Î¼] := by rw [covariance_smul_left]; simp

@[simp]
lemma covariance_fun_neg_left : cov[fun Ï‰ â†¦ - X Ï‰, Y; Î¼] = -cov[X, Y; Î¼] :=
  covariance_neg_left

@[simp]
lemma covariance_neg_right : cov[X, -Y; Î¼] = -cov[X, Y; Î¼] := by
  calc cov[X, -Y; Î¼]
  _ = cov[X, (-1 : â„) â€¢ Y; Î¼] := by simp
  _ = - cov[X, Y; Î¼] := by rw [covariance_smul_right]; simp

@[simp]
lemma covariance_fun_neg_right : cov[X, fun Ï‰ â†¦ - Y Ï‰; Î¼] = -cov[X, Y; Î¼] :=
  covariance_neg_right

lemma covariance_sub_left [IsFiniteMeasure Î¼]
    (hX : MemLp X 2 Î¼) (hY : MemLp Y 2 Î¼) (hZ : MemLp Z 2 Î¼) :
    cov[X - Y, Z; Î¼] = cov[X, Z; Î¼] - cov[Y, Z; Î¼] := by
  simp_rw [sub_eq_add_neg, covariance_add_left hX hY.neg hZ, covariance_neg_left]

lemma covariance_sub_right [IsFiniteMeasure Î¼]
    (hX : MemLp X 2 Î¼) (hY : MemLp Y 2 Î¼) (hZ : MemLp Z 2 Î¼) :
    cov[X, Y - Z; Î¼] = cov[X, Y; Î¼] - cov[X, Z; Î¼] := by
  simp_rw [sub_eq_add_neg, covariance_add_right hX hY hZ.neg, covariance_neg_right]

@[simp]
lemma covariance_sub_const_left [IsProbabilityMeasure Î¼] (hX : Integrable X Î¼) (c : â„) :
    cov[fun Ï‰ â†¦ X Ï‰ - c, Y; Î¼] = cov[X, Y; Î¼] := by
  simp [sub_eq_add_neg, hX]

@[simp]
lemma covariance_const_sub_left [IsProbabilityMeasure Î¼] (hX : Integrable X Î¼) (c : â„) :
    cov[fun Ï‰ â†¦ c - X Ï‰, Y; Î¼] = - cov[X, Y; Î¼] := by
  simp [sub_eq_add_neg, hX.neg']

@[simp]
lemma covariance_sub_const_right [IsProbabilityMeasure Î¼] (hY : Integrable Y Î¼) (c : â„) :
    cov[X, fun Ï‰ â†¦ Y Ï‰ - c; Î¼] = cov[X, Y; Î¼] := by
  simp [sub_eq_add_neg, hY]

@[simp]
lemma covariance_const_sub_right [IsProbabilityMeasure Î¼] (hY : Integrable Y Î¼) (c : â„) :
    cov[X, fun Ï‰ â†¦ c - Y Ï‰; Î¼] = - cov[X, Y; Î¼] := by
  simp [sub_eq_add_neg, hY.neg']

section Sum

variable {Î¹ : Type*} {X : Î¹ â†’ Î© â†’ â„} {s : Finset Î¹} [IsFiniteMeasure Î¼]

lemma covariance_sum_left' (hX : âˆ€ i âˆˆ s, MemLp (X i) 2 Î¼) (hY : MemLp Y 2 Î¼) :
    cov[âˆ‘ i âˆˆ s, X i, Y; Î¼] = âˆ‘ i âˆˆ s, cov[X i, Y; Î¼] := by
  classical
  induction s using Finset.induction with
  | empty => simp
  | insert i s hi h_ind =>
    rw [Finset.sum_insert hi, Finset.sum_insert hi, covariance_add_left, h_ind]
    Â· exact fun j hj â†¦ hX j (by simp [hj])
    Â· exact hX i (by simp)
    Â· exact memLp_finset_sum' s (fun j hj â†¦ hX j (by simp [hj]))
    Â· exact hY

lemma covariance_sum_left [Fintype Î¹] (hX : âˆ€ i, MemLp (X i) 2 Î¼) (hY : MemLp Y 2 Î¼) :
    cov[âˆ‘ i, X i, Y; Î¼] = âˆ‘ i, cov[X i, Y; Î¼] :=
  covariance_sum_left' (fun _ _ â†¦ hX _) hY

lemma covariance_fun_sum_left' (hX : âˆ€ i âˆˆ s, MemLp (X i) 2 Î¼) (hY : MemLp Y 2 Î¼) :
    cov[fun Ï‰ â†¦ âˆ‘ i âˆˆ s, X i Ï‰, Y; Î¼] = âˆ‘ i âˆˆ s, cov[X i, Y; Î¼] := by
  convert covariance_sum_left' hX hY
  simp

lemma covariance_fun_sum_left [Fintype Î¹] (hX : âˆ€ i, MemLp (X i) 2 Î¼) (hY : MemLp Y 2 Î¼) :
    cov[fun Ï‰ â†¦ âˆ‘ i, X i Ï‰, Y; Î¼] = âˆ‘ i, cov[X i, Y; Î¼] := by
  convert covariance_sum_left hX hY
  simp

lemma covariance_sum_right' (hX : âˆ€ i âˆˆ s, MemLp (X i) 2 Î¼) (hY : MemLp Y 2 Î¼) :
    cov[Y, âˆ‘ i âˆˆ s, X i; Î¼] = âˆ‘ i âˆˆ s, cov[Y, X i; Î¼] := by
  rw [covariance_comm, covariance_sum_left' hX hY]
  simp_rw [covariance_comm]

lemma covariance_sum_right [Fintype Î¹] (hX : âˆ€ i, MemLp (X i) 2 Î¼) (hY : MemLp Y 2 Î¼) :
    cov[Y, âˆ‘ i, X i; Î¼] = âˆ‘ i, cov[Y, X i; Î¼] :=
  covariance_sum_right' (fun _ _ â†¦ hX _) hY

lemma covariance_fun_sum_right' (hX : âˆ€ i âˆˆ s, MemLp (X i) 2 Î¼) (hY : MemLp Y 2 Î¼) :
    cov[Y, fun Ï‰ â†¦ âˆ‘ i âˆˆ s, X i Ï‰; Î¼] = âˆ‘ i âˆˆ s, cov[Y, X i; Î¼] := by
  convert covariance_sum_right' hX hY
  simp

lemma covariance_fun_sum_right [Fintype Î¹] (hX : âˆ€ i, MemLp (X i) 2 Î¼) (hY : MemLp Y 2 Î¼) :
    cov[Y, fun Ï‰ â†¦ âˆ‘ i, X i Ï‰; Î¼] = âˆ‘ i, cov[Y, X i; Î¼] :=
  covariance_fun_sum_right' (fun _ _ â†¦ hX _) hY

lemma covariance_sum_sum' {Î¹' : Type*} {Y : Î¹' â†’ Î© â†’ â„} {t : Finset Î¹'}
    (hX : âˆ€ i âˆˆ s, MemLp (X i) 2 Î¼) (hY : âˆ€ i âˆˆ t, MemLp (Y i) 2 Î¼) :
    cov[âˆ‘ i âˆˆ s, X i, âˆ‘ j âˆˆ t, Y j; Î¼] = âˆ‘ i âˆˆ s, âˆ‘ j âˆˆ t, cov[X i, Y j; Î¼] := by
  rw [covariance_sum_left' hX]
  Â· exact Finset.sum_congr rfl fun i hi â†¦ by rw [covariance_sum_right' hY (hX i hi)]
  Â· exact memLp_finset_sum' t hY

lemma covariance_sum_sum [Fintype Î¹] {Î¹' : Type*} [Fintype Î¹'] {Y : Î¹' â†’ Î© â†’ â„}
    (hX : âˆ€ i, MemLp (X i) 2 Î¼) (hY : âˆ€ i, MemLp (Y i) 2 Î¼) :
    cov[âˆ‘ i, X i, âˆ‘ j, Y j; Î¼] = âˆ‘ i, âˆ‘ j, cov[X i, Y j; Î¼] :=
  covariance_sum_sum' (fun _ _ â†¦ hX _) (fun _ _ â†¦ hY _)

lemma covariance_fun_sum_fun_sum' {Î¹' : Type*} {Y : Î¹' â†’ Î© â†’ â„} {t : Finset Î¹'}
    (hX : âˆ€ i âˆˆ s, MemLp (X i) 2 Î¼) (hY : âˆ€ i âˆˆ t, MemLp (Y i) 2 Î¼) :
    cov[fun Ï‰ â†¦ âˆ‘ i âˆˆ s, X i Ï‰, fun Ï‰ â†¦ âˆ‘ j âˆˆ t, Y j Ï‰; Î¼]
      = âˆ‘ i âˆˆ s, âˆ‘ j âˆˆ t, cov[X i, Y j; Î¼] := by
  convert covariance_sum_sum' hX hY
  all_goals simp

lemma covariance_fun_sum_fun_sum [Fintype Î¹] {Î¹' : Type*} [Fintype Î¹'] {Y : Î¹' â†’ Î© â†’ â„}
    (hX : âˆ€ i, MemLp (X i) 2 Î¼) (hY : âˆ€ i, MemLp (Y i) 2 Î¼) :
    cov[fun Ï‰ â†¦ âˆ‘ i, X i Ï‰, fun Ï‰ â†¦ âˆ‘ j, Y j Ï‰; Î¼] = âˆ‘ i, âˆ‘ j, cov[X i, Y j; Î¼] :=
  covariance_fun_sum_fun_sum' (fun _ _ â†¦ hX _) (fun _ _ â†¦ hY _)

end Sum

section Map

variable {Î©' : Type*} {mÎ©' : MeasurableSpace Î©'} {Î¼ : Measure Î©'}

lemma covariance_map_equiv (X Y : Î© â†’ â„) (Z : Î©' â‰ƒáµ Î©) :
    cov[X, Y; Î¼.map Z] = cov[X âˆ˜ Z, Y âˆ˜ Z; Î¼] := by
  simp_rw [covariance, integral_map_equiv, Function.comp_apply]

lemma covariance_map {Z : Î©' â†’ Î©} (hX : AEStronglyMeasurable X (Î¼.map Z))
    (hY : AEStronglyMeasurable Y (Î¼.map Z)) (hZ : AEMeasurable Z Î¼) :
    cov[X, Y; Î¼.map Z] = cov[X âˆ˜ Z, Y âˆ˜ Z; Î¼] := by
  simp_rw [covariance, Function.comp_apply]
  repeat rw [integral_map]
  any_goals assumption
  exact (hX.sub aestronglyMeasurable_const).mul (hY.sub aestronglyMeasurable_const)

lemma covariance_map_fun {Z : Î©' â†’ Î©} (hX : AEStronglyMeasurable X (Î¼.map Z))
    (hY : AEStronglyMeasurable Y (Î¼.map Z)) (hZ : AEMeasurable Z Î¼) :
    cov[X, Y; Î¼.map Z] = cov[fun Ï‰ â†¦ X (Z Ï‰), fun Ï‰ â†¦ Y (Z Ï‰); Î¼] :=
  covariance_map hX hY hZ

end Map

lemma IndepFun.covariance_eq_zero (h : X âŸ‚áµ¢[Î¼] Y) (hX : MemLp X 2 Î¼) (hY : MemLp Y 2 Î¼) :
     cov[X, Y; Î¼] = 0 := by
   by_cases h' : âˆ€áµ Ï‰ âˆ‚Î¼, X Ï‰ = 0
   Â· refine integral_eq_zero_of_ae ?_
     filter_upwards [h'] with Ï‰ hÏ‰
     simp [hÏ‰, integral_eq_zero_of_ae h']
   have := hX.isProbabilityMeasure_of_indepFun X Y (by simp) (by simp) h' h
   rw [covariance_eq_sub hX hY, h.integral_mul_eq_mul_integral
       hX.aestronglyMeasurable hY.aestronglyMeasurable, sub_self]

section Prod

variable {Î©' : Type*} {mÎ©' : MeasurableSpace Î©'} {Î½ : Measure Î©'}
  [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î½] {X : Î© â†’ â„} {Y : Î©' â†’ â„}

lemma covariance_fst_snd_prod (hfÎ¼ : MemLp X 2 Î¼) (hgÎ½ : MemLp Y 2 Î½) :
    cov[fun p â†¦ X p.1, fun p â†¦ Y p.2; Î¼.prod Î½] = 0 :=
  (indepFun_prodâ‚€ hfÎ¼.aemeasurable hgÎ½.aemeasurable).covariance_eq_zero
    (hfÎ¼.comp_fst Î½) (hgÎ½.comp_snd Î¼)

end Prod

end ProbabilityTheory
