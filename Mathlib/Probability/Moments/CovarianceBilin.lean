/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Analysis.LocallyConvex.ContinuousOfBounded
import Mathlib.MeasureTheory.Constructions.BorelSpace.ContinuousLinearMap
import Mathlib.Probability.Moments.Covariance

/-!
# Covariance in Banach spaces

We define the covariance of a finite measure in a Banach space `E`,
as a continuous bilinear form on `Dual â„ E`.

## Main definitions

Let `Î¼` be a finite measure on a normed space `E` with the Borel Ïƒ-algebra. We then define

* `Dual.toLp`: the function `MemLp.toLp` as a continuous linear map from `Dual ğ•œ E` (for `RCLike ğ•œ`)
  into the space `Lp ğ•œ p Î¼` for `p â‰¥ 1`. This needs a hypothesis `MemLp id p Î¼` (we set it to the
  junk value 0 if that's not the case).
* `covarianceBilin` : covariance of a measure `Î¼` with `âˆ« x, â€–xâ€–^2 âˆ‚Î¼ < âˆ` on a Banach space,
  as a continuous bilinear form `Dual â„ E â†’L[â„] Dual â„ E â†’L[â„] â„`.
  If the second moment of `Î¼` is not finite, we set `covarianceBilin Î¼ = 0`.

## Main statements

* `covarianceBilin_apply` : the covariance of `Î¼` on `Lâ‚, Lâ‚‚ : Dual â„ E` is equal to
  `âˆ« x, (Lâ‚ x - Î¼[Lâ‚]) * (Lâ‚‚ x - Î¼[Lâ‚‚]) âˆ‚Î¼`.
* `covarianceBilin_same_eq_variance`: `covarianceBilin Î¼ L L = Var[L; Î¼]`.

## Implementation notes

The hypothesis that `Î¼` has a second moment is written as `MemLp id 2 Î¼` in the code.

-/


open MeasureTheory ProbabilityTheory Complex NormedSpace
open scoped ENNReal NNReal Real Topology

variable {E : Type*} [NormedAddCommGroup E] {mE : MeasurableSpace E} {Î¼ : Measure E} {p : â„â‰¥0âˆ}

namespace NormedSpace.Dual

section LinearMap

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] [NormedSpace ğ•œ E]

open Classical in
/-- Linear map from the dual to `Lp` equal to `MemLp.toLp` if `MemLp id p Î¼` and to 0 otherwise. -/
noncomputable
def toLpâ‚— (Î¼ : Measure E) (p : â„â‰¥0âˆ) :
    Dual ğ•œ E â†’â‚—[ğ•œ] Lp ğ•œ p Î¼ :=
  if h_Lp : MemLp id p Î¼ then
  { toFun := fun L â†¦ MemLp.toLp L (h_Lp.continuousLinearMap_comp L)
    map_add' u v := by push_cast; rw [MemLp.toLp_add]
    map_smul' c L := by push_cast; rw [MemLp.toLp_const_smul]; rfl }
  else 0

@[simp]
lemma toLpâ‚—_apply (h_Lp : MemLp id p Î¼) (L : Dual ğ•œ E) :
    L.toLpâ‚— Î¼ p = MemLp.toLp L (h_Lp.continuousLinearMap_comp L) := by
  simp [toLpâ‚—, dif_pos h_Lp]

@[simp]
lemma toLpâ‚—_of_not_memLp (h_Lp : Â¬ MemLp id p Î¼) (L : Dual ğ•œ E) :
    L.toLpâ‚— Î¼ p = 0 := by
  simp [toLpâ‚—, dif_neg h_Lp]

lemma norm_toLpâ‚—_le [OpensMeasurableSpace E] (L : Dual ğ•œ E) :
    â€–L.toLpâ‚— Î¼ pâ€– â‰¤ â€–Lâ€– * (eLpNorm id p Î¼).toReal := by
  by_cases h_Lp : MemLp id p Î¼
  swap
  Â· simp only [h_Lp, not_false_eq_true, toLpâ‚—_of_not_memLp, Lp.norm_zero]
    positivity
  by_cases hp : p = 0
  Â· simp only [h_Lp, toLpâ‚—_apply, Lp.norm_toLp]
    simp [hp]
  by_cases hp_top : p = âˆ
  Â· simp only [hp_top, Dual.toLpâ‚—_apply h_Lp, Lp.norm_toLp, eLpNorm_exponent_top] at h_Lp âŠ¢
    simp only [eLpNormEssSup, id_eq]
    suffices (essSup (fun x â†¦ â€–L xâ€–â‚‘) Î¼).toReal â‰¤ (essSup (fun x â†¦ â€–Lâ€–â‚‘ *â€–xâ€–â‚‘) Î¼).toReal by
      rwa [ENNReal.essSup_const_mul, ENNReal.toReal_mul, toReal_enorm] at this
    gcongr
    Â· rw [ENNReal.essSup_const_mul]
      exact ENNReal.mul_ne_top (by simp) h_Lp.eLpNorm_ne_top
    Â· exact essSup_mono_ae <| ae_of_all _ L.le_opNorm_enorm
  have h0 : 0 < p.toReal := by simp [ENNReal.toReal_pos_iff, pos_iff_ne_zero, hp, Ne.lt_top hp_top]
  suffices â€–L.toLpâ‚— Î¼ pâ€–
      â‰¤ (â€–Lâ€–â‚‘ ^ p.toReal * âˆ«â» x, â€–xâ€–â‚‘ ^ p.toReal âˆ‚Î¼).toReal ^ p.toRealâ»Â¹ by
    refine this.trans_eq ?_
    simp only [ENNReal.toReal_mul]
    rw [â† ENNReal.toReal_rpow, Real.mul_rpow (by positivity) (by positivity),
      â† Real.rpow_mul (by positivity), mul_inv_cancelâ‚€ h0.ne', Real.rpow_one, toReal_enorm]
    rw [eLpNorm_eq_lintegral_rpow_enorm (by simp [hp]) hp_top, ENNReal.toReal_rpow]
    simp
  rw [Dual.toLpâ‚—_apply h_Lp, Lp.norm_toLp, eLpNorm_eq_lintegral_rpow_enorm (by simp [hp]) hp_top]
  simp only [one_div]
  refine ENNReal.toReal_le_of_le_ofReal (by positivity) ?_
  suffices âˆ«â» x, â€–L xâ€–â‚‘ ^ p.toReal âˆ‚Î¼ â‰¤ â€–Lâ€–â‚‘ ^ p.toReal * âˆ«â» x, â€–xâ€–â‚‘ ^ p.toReal âˆ‚Î¼ by
    rw [â† ENNReal.ofReal_rpow_of_nonneg (by positivity) (by positivity)]
    gcongr
    rwa [ENNReal.ofReal_toReal]
    refine ENNReal.mul_ne_top (by simp) ?_
    have h := h_Lp.eLpNorm_ne_top
    rw [eLpNorm_eq_lintegral_rpow_enorm (by simp [hp]) hp_top] at h
    simpa [h0] using h
  calc âˆ«â» x, â€–L xâ€–â‚‘ ^ p.toReal âˆ‚Î¼
  _ â‰¤ âˆ«â» x, â€–Lâ€–â‚‘ ^ p.toReal * â€–xâ€–â‚‘ ^ p.toReal âˆ‚Î¼ := by
    refine lintegral_mono fun x â†¦ ?_
    rw [â† ENNReal.mul_rpow_of_nonneg]
    swap; Â· positivity
    gcongr
    exact L.le_opNorm_enorm x
  _ = â€–Lâ€–â‚‘ ^ p.toReal * âˆ«â» x, â€–xâ€–â‚‘ ^ p.toReal âˆ‚Î¼ := by rw [lintegral_const_mul]; fun_prop

end LinearMap

section ContinuousLinearMap

variable {ğ•œ : Type*} [RCLike ğ•œ] [NormedSpace ğ•œ E] [OpensMeasurableSpace E]

/-- Continuous linear map from the dual to `Lp` equal to `MemLp.toLp` if `MemLp id p Î¼`
and to 0 otherwise. -/
noncomputable
def toLp (Î¼ : Measure E) (p : â„â‰¥0âˆ) [Fact (1 â‰¤ p)] :
    Dual ğ•œ E â†’L[ğ•œ] Lp ğ•œ p Î¼ where
  toLinearMap := Dual.toLpâ‚— Î¼ p
  cont := by
    refine LinearMap.continuous_of_locally_bounded _ fun s hs â†¦ ?_
    rw [image_isVonNBounded_iff]
    simp_rw [isVonNBounded_iff'] at hs
    obtain âŸ¨r, hxrâŸ© := hs
    refine âŸ¨r * (eLpNorm id p Î¼).toReal, fun L hLs â†¦ ?_âŸ©
    specialize hxr L hLs
    refine (Dual.norm_toLpâ‚—_le L).trans ?_
    gcongr

@[simp]
lemma toLp_apply [Fact (1 â‰¤ p)] (h_Lp : MemLp id p Î¼) (L : Dual ğ•œ E) :
    L.toLp Î¼ p = MemLp.toLp L (h_Lp.continuousLinearMap_comp L) := by
  simp [toLp, h_Lp]

@[simp]
lemma toLp_of_not_memLp [Fact (1 â‰¤ p)] (h_Lp : Â¬ MemLp id p Î¼) (L : Dual ğ•œ E) :
    L.toLp Î¼ p = 0 := by
  simp [toLp, h_Lp]

end ContinuousLinearMap

end NormedSpace.Dual

namespace ProbabilityTheory

section Centered

variable [NormedSpace â„ E] [OpensMeasurableSpace E]

/-- Continuous bilinear form with value `âˆ« x, Lâ‚ x * Lâ‚‚ x âˆ‚Î¼` on `(Lâ‚, Lâ‚‚)`.
This is equal to the covariance only if `Î¼` is centered. -/
noncomputable
def uncenteredCovarianceBilin (Î¼ : Measure E) : Dual â„ E â†’L[â„] Dual â„ E â†’L[â„] â„ :=
  ContinuousLinearMap.bilinearComp (isBoundedBilinearMap_inner (ğ•œ := â„)).toContinuousLinearMap
    (Dual.toLp Î¼ 2) (Dual.toLp Î¼ 2)

lemma uncenteredCovarianceBilin_apply (h : MemLp id 2 Î¼) (Lâ‚ Lâ‚‚ : Dual â„ E) :
    uncenteredCovarianceBilin Î¼ Lâ‚ Lâ‚‚ = âˆ« x, Lâ‚ x * Lâ‚‚ x âˆ‚Î¼ := by
  simp only [uncenteredCovarianceBilin, ContinuousLinearMap.bilinearComp_apply,
    Dual.toLp_apply h, L2.inner_def, RCLike.inner_apply, conj_trivial]
  refine integral_congr_ae ?_
  filter_upwards [MemLp.coeFn_toLp (h.continuousLinearMap_comp Lâ‚),
    MemLp.coeFn_toLp (h.continuousLinearMap_comp Lâ‚‚)] with x hxLâ‚ hxLâ‚‚
  simp only [id_eq] at hxLâ‚ hxLâ‚‚
  rw [hxLâ‚, hxLâ‚‚, mul_comm]

lemma uncenteredCovarianceBilin_of_not_memLp (h : Â¬ MemLp id 2 Î¼) (Lâ‚ Lâ‚‚ : Dual â„ E) :
    uncenteredCovarianceBilin Î¼ Lâ‚ Lâ‚‚ = 0 := by
  simp [uncenteredCovarianceBilin, Dual.toLp_of_not_memLp h]

lemma uncenteredCovarianceBilin_zero : uncenteredCovarianceBilin (0 : Measure E) = 0 := by
  ext
  have : Subsingleton (Lp â„ 2 (0 : Measure E)) := âŸ¨fun x y â†¦ Lp.ext_iff.2 rflâŸ©
  simp [uncenteredCovarianceBilin, Subsingleton.eq_zero (Dual.toLp 0 2)]

lemma norm_uncenteredCovarianceBilin_le (Lâ‚ Lâ‚‚ : Dual â„ E) :
    â€–uncenteredCovarianceBilin Î¼ Lâ‚ Lâ‚‚â€– â‰¤ â€–Lâ‚â€– * â€–Lâ‚‚â€– * âˆ« x, â€–xâ€– ^ 2 âˆ‚Î¼ := by
  by_cases h : MemLp id 2 Î¼
  swap; Â· simp only [uncenteredCovarianceBilin_of_not_memLp h, norm_zero]; positivity
  calc â€–uncenteredCovarianceBilin Î¼ Lâ‚ Lâ‚‚â€–
  _ = â€–âˆ« x, Lâ‚ x * Lâ‚‚ x âˆ‚Î¼â€– := by rw [uncenteredCovarianceBilin_apply h]
  _ â‰¤ âˆ« x, â€–Lâ‚ xâ€– * â€–Lâ‚‚ xâ€– âˆ‚Î¼ := (norm_integral_le_integral_norm _).trans (by simp)
  _ â‰¤ âˆ« x, â€–Lâ‚â€– * â€–xâ€– * â€–Lâ‚‚â€– * â€–xâ€– âˆ‚Î¼ := by
    refine integral_mono_ae ?_ ?_ (ae_of_all _ fun x â†¦ ?_)
    Â· simp_rw [â† norm_mul]
      exact (MemLp.integrable_mul (h.continuousLinearMap_comp Lâ‚)
        (h.continuousLinearMap_comp Lâ‚‚)).norm
    Â· simp_rw [mul_assoc]
      refine Integrable.const_mul ?_ _
      simp_rw [â† mul_assoc, mul_comm _ (â€–Lâ‚‚â€–), mul_assoc, â† pow_two]
      refine Integrable.const_mul ?_ _
      exact h.integrable_norm_pow (by simp)
    Â· simp only
      rw [mul_assoc]
      gcongr
      Â· exact ContinuousLinearMap.le_opNorm Lâ‚ x
      Â· exact ContinuousLinearMap.le_opNorm Lâ‚‚ x
  _ = â€–Lâ‚â€– * â€–Lâ‚‚â€– * âˆ« x, â€–xâ€– ^ 2 âˆ‚Î¼ := by
    rw [â† integral_const_mul]
    congr with x
    ring

end Centered

section Covariance

variable [NormedSpace â„ E] [BorelSpace E] [IsFiniteMeasure Î¼]

open Classical in
/-- Continuous bilinear form with value `âˆ« x, (Lâ‚ x - Î¼[Lâ‚]) * (Lâ‚‚ x - Î¼[Lâ‚‚]) âˆ‚Î¼` on `(Lâ‚, Lâ‚‚)`
if `MemLp id 2 Î¼`. If not, we set it to zero. -/
noncomputable
def covarianceBilin (Î¼ : Measure E) : Dual â„ E â†’L[â„] Dual â„ E â†’L[â„] â„ :=
  uncenteredCovarianceBilin (Î¼.map (fun x â†¦ x - âˆ« x, x âˆ‚Î¼))

@[simp]
lemma covarianceBilin_of_not_memLp (h : Â¬ MemLp id 2 Î¼) (Lâ‚ Lâ‚‚ : Dual â„ E) :
    covarianceBilin Î¼ Lâ‚ Lâ‚‚ = 0 := by
  rw [covarianceBilin, uncenteredCovarianceBilin_of_not_memLp]
  rw [(measurableEmbedding_subRight _).memLp_map_measure_iff]
  refine fun h_Lp â†¦ h ?_
  have : (id : E â†’ E) = fun x â†¦ x - âˆ« x, x âˆ‚Î¼ + âˆ« x, x âˆ‚Î¼ := by ext; simp
  rw [this]
  exact h_Lp.add (memLp_const _)

@[simp]
lemma covarianceBilin_zero : covarianceBilin (0 : Measure E) = 0 := by
  rw [covarianceBilin, Measure.map_zero, uncenteredCovarianceBilin_zero]

lemma covarianceBilin_comm (Lâ‚ Lâ‚‚ : Dual â„ E) :
    covarianceBilin Î¼ Lâ‚ Lâ‚‚ = covarianceBilin Î¼ Lâ‚‚ Lâ‚ := by
  by_cases h : MemLp id 2 Î¼
  Â· have h' : MemLp id 2 (Measure.map (fun x â†¦ x - âˆ« (x : E), x âˆ‚Î¼) Î¼) :=
      (measurableEmbedding_subRight _).memLp_map_measure_iff.mpr <| h.sub (memLp_const _)
    simp_rw [covarianceBilin, uncenteredCovarianceBilin_apply h', mul_comm (Lâ‚ _)]
  Â· simp [h]

variable [CompleteSpace E]

lemma covarianceBilin_apply (h : MemLp id 2 Î¼) (Lâ‚ Lâ‚‚ : Dual â„ E) :
    covarianceBilin Î¼ Lâ‚ Lâ‚‚ = âˆ« x, (Lâ‚ x - Î¼[Lâ‚]) * (Lâ‚‚ x - Î¼[Lâ‚‚]) âˆ‚Î¼ := by
  rw [covarianceBilin, uncenteredCovarianceBilin_apply,
    integral_map (by fun_prop) (by fun_prop)]
  Â· have hL (L : Dual â„ E) : Î¼[L] = L (âˆ« x, x âˆ‚Î¼) := L.integral_comp_comm (h.integrable (by simp))
    simp [â† hL]
  Â· exact (measurableEmbedding_subRight _).memLp_map_measure_iff.mpr <| h.sub (memLp_const _)

lemma covarianceBilin_apply' (h : MemLp id 2 Î¼) (Lâ‚ Lâ‚‚ : Dual â„ E) :
    covarianceBilin Î¼ Lâ‚ Lâ‚‚ = âˆ« x, Lâ‚ (x - Î¼[id]) * Lâ‚‚ (x - Î¼[id]) âˆ‚Î¼ := by
  rw [covarianceBilin_apply h]
  have hL (L : Dual â„ E) : Î¼[L] = L (âˆ« x, x âˆ‚Î¼) := L.integral_comp_comm (h.integrable (by simp))
  simp [â† hL]

lemma covarianceBilin_eq_covariance (h : MemLp id 2 Î¼) (Lâ‚ Lâ‚‚ : Dual â„ E) :
    covarianceBilin Î¼ Lâ‚ Lâ‚‚ = cov[Lâ‚, Lâ‚‚; Î¼] := by
  rw [covarianceBilin_apply h, covariance]

lemma covarianceBilin_self_eq_variance (h : MemLp id 2 Î¼) (L : Dual â„ E) :
    covarianceBilin Î¼ L L = Var[L; Î¼] := by
  rw [covarianceBilin_eq_covariance h, covariance_self (by fun_prop)]

@[deprecated (since := "2025-07-16")] alias covarianceBilin_same_eq_variance :=
  covarianceBilin_self_eq_variance

end Covariance

end ProbabilityTheory
