/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Analysis.LocallyConvex.ContinuousOfBounded
import Mathlib.MeasureTheory.Constructions.BorelSpace.ContinuousLinearMap
import Mathlib.Probability.Variance

/-!
# Covariance in Banach spaces

We define the covariance of a finite measure in a separable Banach space `E`,
as a continous bilinear form on `Dual â„ E`.

## Main definitions

Let `Î¼` be a finite measure on a normed space `E` with the Borel Ïƒ-algebra. We then define

* `Dual.toLp`: the function `MemLp.toLp` as a continuous linear map from
  `Dual ğ•œ E` (for `RCLike ğ•œ`) into the space `Lp ğ•œ p Î¼` for finite `p â‰¥ 1`.
  This needs a hypothesis `MemLp id p Î¼`.
* `covarianceBilin` : covariance of a measure `Î¼` with `âˆ« x, â€–xâ€–^2 âˆ‚Î¼ < âˆ` on a separable Banach
  space, as a continuous bilinear form `Dual â„ E â†’L[â„] Dual â„ E â†’L[â„] â„`.
  If the second moment of `Î¼` is not finite, we set `covarianceBilin Î¼ = 0`.

## Main statements

* `covarianceBilin_apply` : the covariance of `Î¼` on `Lâ‚, Lâ‚‚ : Dual â„ E` is equal to
  `âˆ« x, (Lâ‚ x - Î¼[Lâ‚]) * (Lâ‚‚ x - Î¼[Lâ‚‚]) âˆ‚Î¼`.
* `covarianceBilin_same_eq_variance`: `covarianceBilin Î¼ L L = Var[L; Î¼]`.

## Implementation notes

The hypothesis that `Î¼` has a second moment is written as `MemLp id 2 Î¼` in the code.

-/


open MeasureTheory ProbabilityTheory Complex NormedSpace
open scoped ENNReal NNReal Real Topology

namespace ProbabilityTheory

variable {E : Type*} [NormedAddCommGroup E] {mE : MeasurableSpace E} {Î¼ : Measure E} {p : â„â‰¥0âˆ}

section ToLp

section LinearMap

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] [NormedSpace ğ•œ E]

/-- `MemLp.toLp` as a `LinearMap` from the dual. -/
noncomputable
def ContinuousLinearMap.toLpâ‚— (Î¼ : Measure E) (p : â„â‰¥0âˆ) (h_Lp : MemLp id p Î¼) :
    Dual ğ•œ E â†’â‚—[ğ•œ] Lp ğ•œ p Î¼ where
  toFun := fun L â†¦ MemLp.toLp L (h_Lp.continuousLinearMap_comp L)
  map_add' u v := by push_cast; rw [MemLp.toLp_add]
  map_smul' c L := by push_cast; rw [MemLp.toLp_const_smul]; rfl

@[simp]
lemma ContinuousLinearMap.toLpâ‚—_apply (h_Lp : MemLp id p Î¼) (L : Dual ğ•œ E) :
    L.toLpâ‚— Î¼ p h_Lp = MemLp.toLp L (h_Lp.continuousLinearMap_comp L) := rfl

lemma norm_toLpâ‚—_le [OpensMeasurableSpace E]
    (h_Lp : MemLp id p Î¼) (L : Dual ğ•œ E) (hp : p â‰  0) (hp_top : p â‰  âˆ) :
    â€–L.toLpâ‚— Î¼ p h_Lpâ€– â‰¤ â€–Lâ€– * (eLpNorm id p Î¼).toReal := by
  have h0 : 0 < p.toReal := by simp [ENNReal.toReal_pos_iff, pos_iff_ne_zero, hp, hp_top.lt_top]
  suffices â€–L.toLpâ‚— Î¼ p h_Lpâ€–
      â‰¤ (â€–Lâ€–â‚‘ ^ p.toReal * âˆ«â» x, â€–xâ€–â‚‘ ^ p.toReal âˆ‚Î¼).toReal ^ p.toRealâ»Â¹ by
    refine this.trans_eq ?_
    simp only [ENNReal.toReal_mul]
    rw [â† ENNReal.toReal_rpow, Real.mul_rpow (by positivity) (by positivity),
      â† Real.rpow_mul (by positivity), mul_inv_cancelâ‚€ h0.ne', Real.rpow_one, toReal_enorm]
    rw [eLpNorm_eq_lintegral_rpow_enorm (by simp [hp]) hp_top, ENNReal.toReal_rpow]
    simp
  rw [ContinuousLinearMap.toLpâ‚—_apply, Lp.norm_toLp,
    eLpNorm_eq_lintegral_rpow_enorm (by simp [hp]) hp_top]
  simp only [ENNReal.toReal_ofNat, ENNReal.rpow_ofNat, one_div]
  refine ENNReal.toReal_le_of_le_ofReal (by positivity) ?_
  suffices âˆ«â» x, â€–L xâ€–â‚‘ ^ p.toReal âˆ‚Î¼ â‰¤ â€–Lâ€–â‚‘ ^ p.toReal * âˆ«â» x, â€–xâ€–â‚‘ ^ p.toReal âˆ‚Î¼ by
    rw [â† ENNReal.ofReal_rpow_of_nonneg (by positivity) (by positivity)]
    gcongr
    rwa [ENNReal.ofReal_toReal]
    refine ENNReal.mul_ne_top (by simp) ?_
    have h := h_Lp.eLpNorm_ne_top
    rw [eLpNorm_eq_lintegral_rpow_enorm (by simp [hp]) hp_top] at h
    simpa [h0] using h
  calc âˆ«â» x, â€–L xâ€–â‚‘ ^ p.toReal âˆ‚Î¼
  _ â‰¤ âˆ«â» x, â€–Lâ€–â‚‘ ^ p.toReal * â€–xâ€–â‚‘ ^ p.toReal âˆ‚Î¼ := by
    refine lintegral_mono fun x â†¦ ?_
    rw [â† ENNReal.mul_rpow_of_nonneg]
    swap; Â· positivity
    gcongr
    simp_rw [â† ofReal_norm]
    rw [â† ENNReal.ofReal_mul (by positivity)]
    gcongr
    exact L.le_opNorm x
  _ = â€–Lâ€–â‚‘ ^ p.toReal * âˆ«â» x, â€–xâ€–â‚‘ ^ p.toReal âˆ‚Î¼ := by rw [lintegral_const_mul]; fun_prop

end LinearMap

section ContinuousLinearMap

variable {ğ•œ : Type*} [RCLike ğ•œ] [NormedSpace ğ•œ E] [OpensMeasurableSpace E]

/-- `MemLp.toLp` as a continuous linear map from `Dual ğ•œ E` to `Lp ğ•œ p Î¼`. -/
noncomputable
def _root_.NormedSpace.Dual.toLp (Î¼ : Measure E) (p : â„â‰¥0âˆ) [Fact (1 â‰¤ p)] (h_Lp : MemLp id p Î¼)
    (hp : p â‰  âˆ) :
    Dual ğ•œ E â†’L[ğ•œ] Lp ğ•œ p Î¼ where
  toLinearMap := ContinuousLinearMap.toLpâ‚— Î¼ p h_Lp
  cont := by
    refine LinearMap.continuous_of_locally_bounded _ fun s hs â†¦ ?_
    rw [image_isVonNBounded_iff]
    simp_rw [isVonNBounded_iff'] at hs
    obtain âŸ¨r, hxrâŸ© := hs
    refine âŸ¨r * (eLpNorm id p Î¼).toReal, fun L hLs â†¦ ?_âŸ©
    specialize hxr L hLs
    have hp_ne : p â‰  0 := by
      have : 1 â‰¤ p := Fact.out
      positivity
    refine (norm_toLpâ‚—_le h_Lp L hp_ne hp).trans ?_
    gcongr

@[simp]
lemma Dual.toLp_apply [Fact (1 â‰¤ p)] (h_Lp : MemLp id p Î¼) (hp : p â‰  âˆ) (L : Dual ğ•œ E) :
    L.toLp Î¼ p h_Lp hp = MemLp.toLp L (h_Lp.continuousLinearMap_comp L) := rfl

end ContinuousLinearMap

end ToLp

section Centered

variable [NormedSpace â„ E] [OpensMeasurableSpace E]

/-- Continuous bilinear form with value `âˆ« x, Lâ‚ x * Lâ‚‚ x âˆ‚Î¼` on `(Lâ‚, Lâ‚‚)`.
This is equal to the covariance only if `Î¼` is centered. -/
noncomputable
def centeredCovarianceBilin (Î¼ : Measure E) (h : MemLp id 2 Î¼) :
    Dual â„ E â†’L[â„] Dual â„ E â†’L[â„] â„ :=
  ContinuousLinearMap.bilinearComp (isBoundedBilinearMap_inner (ğ•œ := â„)).toContinuousLinearMap
    (Dual.toLp Î¼ 2 h (by simp)) (Dual.toLp Î¼ 2 h (by simp))

lemma centeredCovarianceBilin_apply (h : MemLp id 2 Î¼) (Lâ‚ Lâ‚‚ : Dual â„ E) :
    centeredCovarianceBilin Î¼ h Lâ‚ Lâ‚‚ = âˆ« x, Lâ‚ x * Lâ‚‚ x âˆ‚Î¼ := by
  simp only [centeredCovarianceBilin, ContinuousLinearMap.bilinearComp_apply,
    Dual.toLp_apply, L2.inner_def, RCLike.inner_apply, conj_trivial]
  refine integral_congr_ae ?_
  filter_upwards [MemLp.coeFn_toLp (h.continuousLinearMap_comp Lâ‚),
    MemLp.coeFn_toLp (h.continuousLinearMap_comp Lâ‚‚)] with x hxLâ‚ hxLâ‚‚
  simp only [id_eq] at hxLâ‚ hxLâ‚‚
  rw [hxLâ‚, hxLâ‚‚, mul_comm]

lemma norm_centeredCovarianceBilin_le (h : MemLp id 2 Î¼) (Lâ‚ Lâ‚‚ : Dual â„ E) :
    â€–centeredCovarianceBilin Î¼ h Lâ‚ Lâ‚‚â€– â‰¤ â€–Lâ‚â€– * â€–Lâ‚‚â€– * âˆ« x, â€–xâ€– ^ 2 âˆ‚Î¼ := by
  calc â€–centeredCovarianceBilin Î¼ h Lâ‚ Lâ‚‚â€–
  _ = â€–âˆ« x, Lâ‚ x * Lâ‚‚ x âˆ‚Î¼â€– := by rw [centeredCovarianceBilin_apply]
  _ â‰¤ âˆ« x, â€–Lâ‚ xâ€– * â€–Lâ‚‚ xâ€– âˆ‚Î¼ := (norm_integral_le_integral_norm _).trans (by simp)
  _ â‰¤ âˆ« x, â€–Lâ‚â€– * â€–xâ€– * â€–Lâ‚‚â€– * â€–xâ€– âˆ‚Î¼ := by
    refine integral_mono_ae ?_ ?_ (ae_of_all _ fun x â†¦ ?_)
    Â· simp_rw [â† norm_mul]
      exact (MemLp.integrable_mul (h.continuousLinearMap_comp Lâ‚)
        (h.continuousLinearMap_comp Lâ‚‚)).norm
    Â· simp_rw [mul_assoc]
      refine Integrable.const_mul ?_ _
      simp_rw [â† mul_assoc, mul_comm _ (â€–Lâ‚‚â€–), mul_assoc, â† pow_two]
      refine Integrable.const_mul ?_ _
      exact h.integrable_norm_pow (by simp)
    Â· simp only
      rw [mul_assoc]
      gcongr
      Â· exact ContinuousLinearMap.le_opNorm Lâ‚ x
      Â· exact ContinuousLinearMap.le_opNorm Lâ‚‚ x
  _ = â€–Lâ‚â€– * â€–Lâ‚‚â€– * âˆ« x, â€–xâ€– ^ 2 âˆ‚Î¼ := by
    rw [â† integral_const_mul]
    congr with x
    ring

end Centered

section Covariance

variable [NormedSpace â„ E] [BorelSpace E] [SecondCountableTopology E] [IsFiniteMeasure Î¼]

open Classical in
/-- Continuous bilinear form with value `âˆ« x, (Lâ‚ x - Î¼[Lâ‚]) * (Lâ‚‚ x - Î¼[Lâ‚‚]) âˆ‚Î¼` on `(Lâ‚, Lâ‚‚)`
if `MemLp id 2 Î¼`. If not, we set it to zero. -/
noncomputable
def covarianceBilin (Î¼ : Measure E) [IsFiniteMeasure Î¼] :
    Dual â„ E â†’L[â„] Dual â„ E â†’L[â„] â„ :=
  if h : MemLp id 2 Î¼ then
    centeredCovarianceBilin (Î¼.map (fun x â†¦ x - âˆ« x, x âˆ‚Î¼))
      ((memLp_map_measure_iff (by fun_prop) (by fun_prop)).mpr <| h.sub (memLp_const _))
  else 0

lemma covarianceBilin_of_memLp (h : MemLp id 2 Î¼) (Lâ‚ Lâ‚‚ : Dual â„ E) :
    covarianceBilin Î¼ Lâ‚ Lâ‚‚ = centeredCovarianceBilin (Î¼.map (fun x â†¦ x - âˆ« x, x âˆ‚Î¼))
      ((memLp_map_measure_iff (by fun_prop) (by fun_prop)).mpr <| h.sub (memLp_const _)) Lâ‚ Lâ‚‚ := by
  rw [covarianceBilin, dif_pos h]

lemma covarianceBilin_of_not_memLp (h : Â¬ MemLp id 2 Î¼) (Lâ‚ Lâ‚‚ : Dual â„ E) :
    covarianceBilin Î¼ Lâ‚ Lâ‚‚ = 0 := by
  simp [covarianceBilin, dif_neg h]

variable [CompleteSpace E]

lemma covarianceBilin_apply (h : MemLp id 2 Î¼) (Lâ‚ Lâ‚‚ : Dual â„ E) :
    covarianceBilin Î¼ Lâ‚ Lâ‚‚ = âˆ« x, (Lâ‚ x - Î¼[Lâ‚]) * (Lâ‚‚ x - Î¼[Lâ‚‚]) âˆ‚Î¼ := by
  rw [covarianceBilin_of_memLp h, centeredCovarianceBilin_apply,
    integral_map (by fun_prop) (by fun_prop)]
  have hL (L : Dual â„ E) : Î¼[L] = L (âˆ« x, x âˆ‚Î¼) := L.integral_comp_comm (h.integrable (by simp))
  simp [â† hL]

lemma covarianceBilin_same_eq_variance (h : MemLp id 2 Î¼) (L : Dual â„ E) :
    covarianceBilin Î¼ L L = Var[L; Î¼] := by
  rw [covarianceBilin_apply h, variance_eq_integral (by fun_prop)]
  simp_rw [pow_two]

end Covariance

end ProbabilityTheory
