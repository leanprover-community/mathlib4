/-
Copyright (c) 2022 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Kexing Ying
-/
import Mathlib.Probability.Moments.Covariance

/-!
# Variance of random variables

We define the variance of a real-valued random variable as `Var[X] = ğ”¼[(X - ğ”¼[X])^2]` (in the
`ProbabilityTheory` locale).

## Main definitions

* `ProbabilityTheory.evariance`: the variance of a real-valued random variable as an extended
  non-negative real.
* `ProbabilityTheory.variance`: the variance of a real-valued random variable as a real number.

## Main results

* `ProbabilityTheory.variance_le_expectation_sq`: the inequality `Var[X] â‰¤ ğ”¼[X^2]`.
* `ProbabilityTheory.meas_ge_le_variance_div_sq`: Chebyshev's inequality, i.e.,
      `â„™ {Ï‰ | c â‰¤ |X Ï‰ - ğ”¼[X]|} â‰¤ ENNReal.ofReal (Var[X] / c ^ 2)`.
* `ProbabilityTheory.meas_ge_le_evariance_div_sq`: Chebyshev's inequality formulated with
  `evariance` without requiring the random variables to be LÂ².
* `ProbabilityTheory.IndepFun.variance_add`: the variance of the sum of two independent
  random variables is the sum of the variances.
* `ProbabilityTheory.IndepFun.variance_sum`: the variance of a finite sum of pairwise
  independent random variables is the sum of the variances.
* `ProbabilityTheory.variance_le_sub_mul_sub`: the variance of a random variable `X` satisfying
  `a â‰¤ X â‰¤ b` almost everywhere is at most `(b - ğ”¼ X) * (ğ”¼ X - a)`.
* `ProbabilityTheory.variance_le_sq_of_bounded`: the variance of a random variable `X` satisfying
  `a â‰¤ X â‰¤ b` almost everywhere is at most`((b - a) / 2) ^ 2`.
-/

open MeasureTheory Filter Finset

noncomputable section

open scoped MeasureTheory ProbabilityTheory ENNReal NNReal

namespace ProbabilityTheory
variable {Î© : Type*} {mÎ© : MeasurableSpace Î©} {X Y : Î© â†’ â„} {Î¼ : Measure Î©}

variable (X Î¼) in
-- Porting note: Consider if `evariance` or `eVariance` is better. Also,
-- consider `eVariationOn` in `Mathlib.Analysis.BoundedVariation`.
/-- The `â„â‰¥0âˆ`-valued variance of a real-valued random variable defined as the Lebesgue integral of
`â€–X - ğ”¼[X]â€–^2`. -/
def evariance : â„â‰¥0âˆ := âˆ«â» Ï‰, â€–X Ï‰ - Î¼[X]â€–â‚‘ ^ 2 âˆ‚Î¼

variable (X Î¼) in
/-- The `â„`-valued variance of a real-valued random variable defined by applying `ENNReal.toReal`
to `evariance`. -/
def variance : â„ := (evariance X Î¼).toReal

/-- The `â„â‰¥0âˆ`-valued variance of the real-valued random variable `X` according to the measure `Î¼`.

This is defined as the Lebesgue integral of `(X - ğ”¼[X])^2`. -/
scoped notation "eVar[" X "; " Î¼ "]" => ProbabilityTheory.evariance X Î¼

/-- The `â„â‰¥0âˆ`-valued variance of the real-valued random variable `X` according to the volume
measure.

This is defined as the Lebesgue integral of `(X - ğ”¼[X])^2`. -/
scoped notation "eVar[" X "]" => eVar[X; MeasureTheory.MeasureSpace.volume]

/-- The `â„`-valued variance of the real-valued random variable `X` according to the measure `Î¼`.

It is set to `0` if `X` has infinite variance. -/
scoped notation "Var[" X "; " Î¼ "]" => ProbabilityTheory.variance X Î¼

/-- The `â„`-valued variance of the real-valued random variable `X` according to the volume measure.

It is set to `0` if `X` has infinite variance. -/
scoped notation "Var[" X "]" => Var[X; MeasureTheory.MeasureSpace.volume]

theorem evariance_congr (h : X =áµ[Î¼] Y) : eVar[X; Î¼] = eVar[Y; Î¼] := by
  simp_rw [evariance, integral_congr_ae h]
  apply lintegral_congr_ae
  filter_upwards [h] with Ï‰ hÏ‰ using by simp [hÏ‰]

theorem variance_congr (h : X =áµ[Î¼] Y) : Var[X; Î¼] = Var[Y; Î¼] := by
  simp_rw [variance, evariance_congr h]

theorem evariance_lt_top [IsFiniteMeasure Î¼] (hX : MemLp X 2 Î¼) : evariance X Î¼ < âˆ := by
  have := ENNReal.pow_lt_top (hX.sub <| memLp_const <| Î¼[X]).2 (n := 2)
  rw [eLpNorm_eq_lintegral_rpow_enorm two_ne_zero ENNReal.ofNat_ne_top, â† ENNReal.rpow_two] at this
  simp only [ENNReal.toReal_ofNat, Pi.sub_apply, one_div] at this
  rw [â† ENNReal.rpow_mul, inv_mul_cancelâ‚€ (two_ne_zero : (2 : â„) â‰  0), ENNReal.rpow_one] at this
  simp_rw [ENNReal.rpow_two] at this
  exact this

lemma evariance_ne_top [IsFiniteMeasure Î¼] (hX : MemLp X 2 Î¼) : evariance X Î¼ â‰  âˆ :=
  (evariance_lt_top hX).ne

theorem evariance_eq_top [IsFiniteMeasure Î¼] (hXm : AEStronglyMeasurable X Î¼) (hX : Â¬MemLp X 2 Î¼) :
    evariance X Î¼ = âˆ := by
  by_contra h
  rw [â† Ne, â† lt_top_iff_ne_top] at h
  have : MemLp (fun Ï‰ => X Ï‰ - Î¼[X]) 2 Î¼ := by
    refine âŸ¨by fun_prop, ?_âŸ©
    rw [eLpNorm_eq_lintegral_rpow_enorm two_ne_zero ENNReal.ofNat_ne_top]
    simp only [ENNReal.toReal_ofNat, ENNReal.rpow_two]
    exact ENNReal.rpow_lt_top_of_nonneg (by linarith) h.ne
  refine hX ?_
  convert this.add (memLp_const Î¼[X])
  ext Ï‰
  rw [Pi.add_apply, sub_add_cancel]

theorem evariance_lt_top_iff_memLp [IsFiniteMeasure Î¼] (hX : AEStronglyMeasurable X Î¼) :
    evariance X Î¼ < âˆ â†” MemLp X 2 Î¼ where
  mp := by contrapose!; rw [top_le_iff]; exact evariance_eq_top hX
  mpr := evariance_lt_top

@[deprecated (since := "2025-02-21")]
alias evariance_lt_top_iff_memâ„’p := evariance_lt_top_iff_memLp

lemma evariance_eq_top_iff [IsFiniteMeasure Î¼] (hX : AEStronglyMeasurable X Î¼) :
    evariance X Î¼ = âˆ â†” Â¬ MemLp X 2 Î¼ := by simp [â† evariance_lt_top_iff_memLp hX]

lemma variance_of_not_memLp [IsFiniteMeasure Î¼] (hX : AEStronglyMeasurable X Î¼)
    (hX_not : Â¬ MemLp X 2 Î¼) :
    variance X Î¼ = 0 := by simp [variance, (evariance_eq_top_iff hX).mpr hX_not]

theorem ofReal_variance [IsFiniteMeasure Î¼] (hX : MemLp X 2 Î¼) :
    .ofReal (variance X Î¼) = evariance X Î¼ := by
  rw [variance, ENNReal.ofReal_toReal]
  exact evariance_ne_top hX

protected alias _root_.MeasureTheory.MemLp.evariance_lt_top := evariance_lt_top
protected alias _root_.MeasureTheory.MemLp.evariance_ne_top := evariance_ne_top
protected alias _root_.MeasureTheory.MemLp.ofReal_variance_eq := ofReal_variance

@[deprecated (since := "2025-02-21")]
protected alias _root_.MeasureTheory.Memâ„’p.evariance_lt_top := evariance_lt_top
@[deprecated (since := "2025-02-21")]
protected alias _root_.MeasureTheory.Memâ„’p.evariance_ne_top := evariance_ne_top
@[deprecated (since := "2025-02-21")]
protected alias _root_.MeasureTheory.Memâ„’p.ofReal_variance_eq := ofReal_variance

variable (X Î¼) in
theorem evariance_eq_lintegral_ofReal :
    evariance X Î¼ = âˆ«â» Ï‰, ENNReal.ofReal ((X Ï‰ - Î¼[X]) ^ 2) âˆ‚Î¼ := by
  simp [evariance, â† enorm_pow, Real.enorm_of_nonneg (sq_nonneg _)]

lemma variance_eq_integral (hX : AEMeasurable X Î¼) : Var[X; Î¼] = âˆ« Ï‰, (X Ï‰ - Î¼[X]) ^ 2 âˆ‚Î¼ := by
  simp [variance, evariance, toReal_enorm, â† integral_toReal ((hX.sub_const _).enorm.pow_const _) <|
    .of_forall fun _ â†¦ ENNReal.pow_lt_top enorm_lt_top]

lemma variance_of_integral_eq_zero (hX : AEMeasurable X Î¼) (hXint : Î¼[X] = 0) :
    variance X Î¼ = âˆ« Ï‰, X Ï‰ ^ 2 âˆ‚Î¼ := by
  simp [variance_eq_integral hX, hXint]

@[deprecated (since := "2025-01-23")]
alias _root_.MeasureTheory.Memâ„’p.variance_eq := variance_eq_integral

@[deprecated (since := "2025-01-23")]
alias _root_.MeasureTheory.Memâ„’p.variance_eq_of_integral_eq_zero := variance_of_integral_eq_zero

@[simp]
theorem evariance_zero : evariance 0 Î¼ = 0 := by simp [evariance]

theorem evariance_eq_zero_iff (hX : AEMeasurable X Î¼) :
    evariance X Î¼ = 0 â†” X =áµ[Î¼] fun _ => Î¼[X] := by
  simp [evariance, lintegral_eq_zero_iff' ((hX.sub_const _).enorm.pow_const _), EventuallyEq,
    sub_eq_zero]

theorem evariance_mul (c : â„) (X : Î© â†’ â„) (Î¼ : Measure Î©) :
    evariance (fun Ï‰ => c * X Ï‰) Î¼ = ENNReal.ofReal (c ^ 2) * evariance X Î¼ := by
  rw [evariance, evariance, â† lintegral_const_mul' _ _ ENNReal.ofReal_lt_top.ne]
  congr with Ï‰
  rw [integral_const_mul, â† mul_sub, enorm_mul, mul_pow, â† enorm_pow,
    Real.enorm_of_nonneg (sq_nonneg _)]

@[simp]
theorem variance_zero (Î¼ : Measure Î©) : variance 0 Î¼ = 0 := by
  simp only [variance, evariance_zero, ENNReal.toReal_zero]

lemma covariance_self {X : Î© â†’ â„} (hX : AEMeasurable X Î¼) :
    cov[X, X; Î¼] = Var[X; Î¼] := by
  rw [covariance, variance_eq_integral hX]
  congr with x
  ring

@[deprecated (since := "2025-06-25")] alias covariance_same := covariance_self

theorem variance_nonneg (X : Î© â†’ â„) (Î¼ : Measure Î©) : 0 â‰¤ variance X Î¼ :=
  ENNReal.toReal_nonneg

theorem variance_mul (c : â„) (X : Î© â†’ â„) (Î¼ : Measure Î©) :
    variance (fun Ï‰ => c * X Ï‰) Î¼ = c ^ 2 * variance X Î¼ := by
  rw [variance, evariance_mul, ENNReal.toReal_mul, ENNReal.toReal_ofReal (sq_nonneg _)]
  rfl

theorem variance_smul (c : â„) (X : Î© â†’ â„) (Î¼ : Measure Î©) :
    variance (c â€¢ X) Î¼ = c ^ 2 * variance X Î¼ :=
  variance_mul c X Î¼

theorem variance_smul' {A : Type*} [CommSemiring A] [Algebra A â„] (c : A) (X : Î© â†’ â„)
    (Î¼ : Measure Î©) : variance (c â€¢ X) Î¼ = c ^ 2 â€¢ variance X Î¼ := by
  convert variance_smul (algebraMap A â„ c) X Î¼ using 1
  Â· congr; simp only [algebraMap_smul]
  Â· simp only [Algebra.smul_def, map_pow]

theorem variance_def' [IsProbabilityMeasure Î¼] {X : Î© â†’ â„} (hX : MemLp X 2 Î¼) :
    variance X Î¼ = Î¼[X ^ 2] - Î¼[X] ^ 2 := by
  simp only [variance_eq_integral hX.aestronglyMeasurable.aemeasurable, sub_sq']
  rw [integral_sub, integral_add]; rotate_left
  Â· exact hX.integrable_sq
  Â· apply integrable_const
  Â· apply hX.integrable_sq.add
    apply integrable_const
  Â· exact ((hX.integrable one_le_two).const_mul 2).mul_const' _
  simp only [integral_const, measureReal_univ_eq_one, smul_eq_mul, one_mul, integral_mul_const,
    integral_const_mul, Pi.pow_apply]
  ring

lemma variance_add_const [IsProbabilityMeasure Î¼] (hX : AEStronglyMeasurable X Î¼) (c : â„) :
    Var[fun Ï‰ â†¦ X Ï‰ + c; Î¼] = Var[X; Î¼] := by
  by_cases hX_Lp : MemLp X 2 Î¼
  Â· have hX_int : Integrable X Î¼ := hX_Lp.integrable one_le_two
    rw [variance_eq_integral (hX.add_const _).aemeasurable,
      integral_add hX_int (by fun_prop), integral_const, variance_eq_integral hX.aemeasurable]
    simp
  Â· rw [variance_of_not_memLp (hX.add_const _), variance_of_not_memLp hX hX_Lp]
    refine fun h_memLp â†¦ hX_Lp ?_
    have : X = fun Ï‰ â†¦ X Ï‰ + c - c := by ext; ring
    rw [this]
    exact h_memLp.sub (memLp_const c)

lemma variance_const_add [IsProbabilityMeasure Î¼] (hX : AEStronglyMeasurable X Î¼) (c : â„) :
    Var[fun Ï‰ â†¦ c + X Ï‰; Î¼] = Var[X; Î¼] := by
  simp_rw [add_comm c, variance_add_const hX c]

lemma variance_fun_neg : Var[fun Ï‰ â†¦ -X Ï‰; Î¼] = Var[X; Î¼] := by
  convert variance_mul (-1) X Î¼
  Â· ext; ring
  Â· simp

lemma variance_neg : Var[-X; Î¼] = Var[X; Î¼] := variance_fun_neg

lemma variance_sub_const [IsProbabilityMeasure Î¼] (hX : AEStronglyMeasurable X Î¼) (c : â„) :
    Var[fun Ï‰ â†¦ X Ï‰ - c; Î¼] = Var[X; Î¼] := by
  simp_rw [sub_eq_add_neg, variance_add_const hX (-c)]

lemma variance_const_sub [IsProbabilityMeasure Î¼] (hX : AEStronglyMeasurable X Î¼) (c : â„) :
    Var[fun Ï‰ â†¦ c - X Ï‰; Î¼] = Var[X; Î¼] := by
  simp_rw [sub_eq_add_neg]
  rw [variance_const_add (by fun_prop) c, variance_fun_neg]

lemma variance_add [IsFiniteMeasure Î¼] (hX : MemLp X 2 Î¼) (hY : MemLp Y 2 Î¼) :
    Var[X + Y; Î¼] = Var[X; Î¼] + 2 * cov[X, Y; Î¼] + Var[Y; Î¼] := by
  rw [â† covariance_self, covariance_add_left hX hY (hX.add hY), covariance_add_right hX hX hY,
    covariance_add_right hY hX hY, covariance_self, covariance_self, covariance_comm]
  Â· ring
  Â· exact hY.aemeasurable
  Â· exact hX.aemeasurable
  Â· exact hX.aemeasurable.add hY.aemeasurable

lemma variance_fun_add [IsFiniteMeasure Î¼] (hX : MemLp X 2 Î¼) (hY : MemLp Y 2 Î¼) :
    Var[fun Ï‰ â†¦ X Ï‰ + Y Ï‰; Î¼] = Var[X; Î¼] + 2 * cov[X, Y; Î¼] + Var[Y; Î¼] :=
  variance_add hX hY

lemma variance_sub [IsFiniteMeasure Î¼] (hX : MemLp X 2 Î¼) (hY : MemLp Y 2 Î¼) :
     Var[X - Y; Î¼] = Var[X; Î¼] - 2 * cov[X, Y; Î¼] + Var[Y; Î¼] := by
   rw [sub_eq_add_neg, variance_add hX hY.neg, variance_neg, covariance_neg_right]
   ring

lemma variance_fun_sub [IsFiniteMeasure Î¼] (hX : MemLp X 2 Î¼) (hY : MemLp Y 2 Î¼) :
    Var[fun Ï‰ â†¦ X Ï‰ - Y Ï‰; Î¼] = Var[X; Î¼] - 2 * cov[X, Y; Î¼] + Var[Y; Î¼] :=
  variance_sub hX hY

variable {Î¹ : Type*} {s : Finset Î¹} {X : (i : Î¹) â†’ Î© â†’ â„}

lemma variance_sum' [IsFiniteMeasure Î¼] (hX : âˆ€ i âˆˆ s, MemLp (X i) 2 Î¼) :
    Var[âˆ‘ i âˆˆ s, X i; Î¼] = âˆ‘ i âˆˆ s, âˆ‘ j âˆˆ s, cov[X i, X j; Î¼] := by
  rw [â† covariance_self, covariance_sum_left' (by simpa)]
  Â· refine Finset.sum_congr rfl fun i hi â†¦ ?_
    rw [covariance_sum_right' (by simpa) (hX i hi)]
  Â· exact memLp_finset_sum' _ (by simpa)
  Â· exact (memLp_finset_sum' _ (by simpa)).aemeasurable

lemma variance_sum [IsFiniteMeasure Î¼] [Fintype Î¹] (hX : âˆ€ i, MemLp (X i) 2 Î¼) :
    Var[âˆ‘ i, X i; Î¼] = âˆ‘ i, âˆ‘ j, cov[X i, X j; Î¼] :=
  variance_sum' (fun _ _ â†¦ hX _)

lemma variance_fun_sum' [IsFiniteMeasure Î¼] (hX : âˆ€ i âˆˆ s, MemLp (X i) 2 Î¼) :
    Var[fun Ï‰ â†¦ âˆ‘ i âˆˆ s, X i Ï‰; Î¼] = âˆ‘ i âˆˆ s, âˆ‘ j âˆˆ s, cov[X i, X j; Î¼] := by
  convert variance_sum' hX
  simp

lemma variance_fun_sum [IsFiniteMeasure Î¼] [Fintype Î¹] (hX : âˆ€ i, MemLp (X i) 2 Î¼) :
    Var[fun Ï‰ â†¦ âˆ‘ i, X i Ï‰; Î¼] = âˆ‘ i, âˆ‘ j, cov[X i, X j; Î¼] := by
  convert variance_sum hX
  simp

variable {X : Î© â†’ â„}

@[simp]
lemma variance_dirac [MeasurableSingletonClass Î©] (x : Î©) : Var[X; Measure.dirac x] = 0 := by
  rw [variance_eq_integral]
  Â· simp
  Â· exact aemeasurable_dirac

lemma variance_map {Î©' : Type*} {mÎ©' : MeasurableSpace Î©'} {Î¼ : Measure Î©'}
    {Y : Î©' â†’ Î©} (hX : AEMeasurable X (Î¼.map Y)) (hY : AEMeasurable Y Î¼) :
    Var[X; Î¼.map Y] = Var[X âˆ˜ Y; Î¼] := by
  rw [variance_eq_integral hX, integral_map hY, variance_eq_integral (hX.comp_aemeasurable hY),
    integral_map hY]
  Â· congr
  Â· exact hX.aestronglyMeasurable
  Â· refine AEStronglyMeasurable.pow ?_ _
    exact AEMeasurable.aestronglyMeasurable (by fun_prop)

lemma variance_map_equiv {Î©' : Type*} {mÎ©' : MeasurableSpace Î©'} {Î¼ : Measure Î©'}
    (X : Î© â†’ â„) (Y : Î©' â‰ƒáµ Î©) :
    Var[X; Î¼.map Y] = Var[X âˆ˜ Y; Î¼] := by
  simp_rw [variance, evariance, lintegral_map_equiv, integral_map_equiv]
  rfl

lemma variance_id_map (hX : AEMeasurable X Î¼) : Var[id; Î¼.map X] = Var[X; Î¼] := by
  simp [variance_map measurable_id.aemeasurable hX]

theorem variance_le_expectation_sq [IsProbabilityMeasure Î¼] {X : Î© â†’ â„}
    (hm : AEStronglyMeasurable X Î¼) : variance X Î¼ â‰¤ Î¼[X ^ 2] := by
  by_cases hX : MemLp X 2 Î¼
  Â· rw [variance_def' hX]
    simp only [sq_nonneg, sub_le_self_iff]
  rw [variance, evariance_eq_lintegral_ofReal, â† integral_eq_lintegral_of_nonneg_ae]
  Â· by_cases hint : Integrable X Î¼; swap
    Â· simp only [integral_undef hint, Pi.pow_apply, sub_zero]
      exact le_rfl
    Â· rw [integral_undef]
      Â· exact integral_nonneg fun a => sq_nonneg _
      intro h
      have A : MemLp (X - fun Ï‰ : Î© => Î¼[X]) 2 Î¼ :=
        (memLp_two_iff_integrable_sq (by fun_prop)).2 h
      have B : MemLp (fun _ : Î© => Î¼[X]) 2 Î¼ := memLp_const _
      apply hX
      convert A.add B
      simp
  Â· exact Eventually.of_forall fun x => sq_nonneg _
  Â· exact (AEMeasurable.pow_const (hm.aemeasurable.sub_const _) _).aestronglyMeasurable

theorem evariance_def' [IsProbabilityMeasure Î¼] {X : Î© â†’ â„} (hX : AEStronglyMeasurable X Î¼) :
    evariance X Î¼ = (âˆ«â» Ï‰, â€–X Ï‰â€–â‚‘ ^ 2 âˆ‚Î¼) - ENNReal.ofReal (Î¼[X] ^ 2) := by
  by_cases hâ„’ : MemLp X 2 Î¼
  Â· rw [â† ofReal_variance hâ„’, variance_def' hâ„’, ENNReal.ofReal_sub _ (sq_nonneg _)]
    congr
    simp_rw [â† enorm_pow, enorm]
    rw [lintegral_coe_eq_integral]
    Â· simp
    Â· simpa using hâ„’.abs.integrable_sq
  Â· symm
    rw [evariance_eq_top hX hâ„’, ENNReal.sub_eq_top_iff]
    refine âŸ¨?_, ENNReal.ofReal_ne_topâŸ©
    rw [MemLp, not_and] at hâ„’
    specialize hâ„’ hX
    simp only [eLpNorm_eq_lintegral_rpow_enorm two_ne_zero ENNReal.ofNat_ne_top, not_lt, top_le_iff,
      ENNReal.toReal_ofNat, one_div, ENNReal.rpow_eq_top_iff, inv_lt_zero, inv_pos, and_true,
      or_iff_not_imp_left, not_and_or, zero_lt_two] at hâ„’
    exact mod_cast hâ„’ fun _ => zero_le_two

set_option linter.deprecated false in
/-- **Chebyshev's inequality** for `â„â‰¥0âˆ`-valued variance. -/
theorem meas_ge_le_evariance_div_sq {X : Î© â†’ â„} (hX : AEStronglyMeasurable X Î¼) {c : â„â‰¥0}
    (hc : c â‰  0) : Î¼ {Ï‰ | â†‘c â‰¤ |X Ï‰ - Î¼[X]|} â‰¤ evariance X Î¼ / c ^ 2 := by
  have A : (c : â„â‰¥0âˆ) â‰  0 := by rwa [Ne, ENNReal.coe_eq_zero]
  have B : AEStronglyMeasurable (fun _ : Î© => Î¼[X]) Î¼ := aestronglyMeasurable_const
  convert meas_ge_le_mul_pow_eLpNorm Î¼ two_ne_zero ENNReal.ofNat_ne_top (hX.sub B) A using 1
  Â· congr
    simp only [Pi.sub_apply, ENNReal.coe_le_coe, â† Real.norm_eq_abs, â† coe_nnnorm,
      NNReal.coe_le_coe]
  Â· rw [eLpNorm_eq_lintegral_rpow_enorm two_ne_zero ENNReal.ofNat_ne_top]
    simp only [ENNReal.toReal_ofNat, one_div, Pi.sub_apply]
    rw [div_eq_mul_inv, ENNReal.inv_pow, mul_comm, ENNReal.rpow_two]
    congr
    simp_rw [â† ENNReal.rpow_mul, inv_mul_cancelâ‚€ (two_ne_zero : (2 : â„) â‰  0), ENNReal.rpow_two,
      ENNReal.rpow_one, evariance]


/-- **Chebyshev's inequality**: one can control the deviation probability of a real random variable
from its expectation in terms of the variance. -/
theorem meas_ge_le_variance_div_sq [IsFiniteMeasure Î¼] {X : Î© â†’ â„} (hX : MemLp X 2 Î¼) {c : â„}
    (hc : 0 < c) : Î¼ {Ï‰ | c â‰¤ |X Ï‰ - Î¼[X]|} â‰¤ ENNReal.ofReal (variance X Î¼ / c ^ 2) := by
  rw [ENNReal.ofReal_div_of_pos (sq_pos_of_ne_zero hc.ne.symm), hX.ofReal_variance_eq]
  convert @meas_ge_le_evariance_div_sq _ _ _ _ hX.1 c.toNNReal (by simp [hc]) using 1
  Â· simp only [Real.coe_toNNReal', max_le_iff, abs_nonneg, and_true]
  Â· rw [ENNReal.ofReal_pow hc.le]
    rfl

-- Porting note: supplied `MeasurableSpace Î©` argument of `h` by unification
/-- The variance of the sum of two independent random variables is the sum of the variances. -/
nonrec theorem IndepFun.variance_add {X Y : Î© â†’ â„} (hX : MemLp X 2 Î¼)
    (hY : MemLp Y 2 Î¼) (h : IndepFun X Y Î¼) : Var[X + Y; Î¼] = Var[X; Î¼] + Var[Y; Î¼] := by
  by_cases h' : X =áµ[Î¼] 0
  Â· rw [variance_congr h', variance_congr h'.add_right]
    simp
  have := hX.isProbabilityMeasure_of_indepFun X Y (by simp) (by simp) h' h
  rw [variance_add hX hY, h.covariance_eq_zero hX hY]
  simp

-- Porting note: supplied `MeasurableSpace Î©` argument of `hs`, `h` by unification
/-- The variance of a finite sum of pairwise independent random variables is the sum of the
variances. -/
nonrec theorem IndepFun.variance_sum {Î¹ : Type*} {X : Î¹ â†’ Î© â†’ â„} {s : Finset Î¹}
    (hs : âˆ€ i âˆˆ s, MemLp (X i) 2 Î¼)
    (h : Set.Pairwise â†‘s fun i j => IndepFun (X i) (X j) Î¼) :
    variance (âˆ‘ i âˆˆ s, X i) Î¼ = âˆ‘ i âˆˆ s, variance (X i) Î¼ := by
  by_cases h'' : âˆ€ i âˆˆ s, X i =áµ[Î¼] 0
  Â· rw [variance_congr (Y := 0), variance_zero]
    Â· symm
      refine Finset.sum_eq_zero fun i hi â†¦ ?_
      simp [variance_congr (h'' i hi)]
    Â· have := fun (i : s) â†¦ h'' i.1 i.2
      filter_upwards [ae_all_iff.2 this] with Ï‰ hÏ‰
      simp only [sum_apply, Pi.zero_apply]
      exact Finset.sum_eq_zero fun i hi â†¦ hÏ‰ âŸ¨i, hiâŸ©
  obtain âŸ¨j, hj1, hj2âŸ© := not_forallâ‚‚.1 h''
  obtain rfl | h' := s.eq_singleton_or_nontrivial hj1
  Â· simp
  obtain âŸ¨k, hk1, hk2âŸ© := h'.exists_ne j
  have := (hs j hj1).isProbabilityMeasure_of_indepFun (X j) (X k) (by simp) (by simp) hj2
    (h hj1 hk1 hk2.symm)
  rw [variance_sum' hs]
  refine Finset.sum_congr rfl (fun i hi â†¦ ?_)
  rw [â† covariance_self (hs i hi).aemeasurable]
  refine Finset.sum_eq_single_of_mem i hi fun j hj1 hj2 â†¦ ?_
  exact (h hi hj1 hj2.symm).covariance_eq_zero (hs i hi) (hs j hj1)

/-- **The Bhatia-Davis inequality on variance**

The variance of a random variable `X` satisfying `a â‰¤ X â‰¤ b` almost everywhere is at most
`(b - ğ”¼ X) * (ğ”¼ X - a)`. -/
lemma variance_le_sub_mul_sub [IsProbabilityMeasure Î¼] {a b : â„} {X : Î© â†’ â„}
    (h : âˆ€áµ Ï‰ âˆ‚Î¼, X Ï‰ âˆˆ Set.Icc a b) (hX : AEMeasurable X Î¼) :
    variance X Î¼ â‰¤ (b - Î¼[X]) * (Î¼[X] - a) := by
  have ha : âˆ€áµ Ï‰ âˆ‚Î¼, a â‰¤ X Ï‰ := h.mono fun Ï‰ h => h.1
  have hb : âˆ€áµ Ï‰ âˆ‚Î¼, X Ï‰ â‰¤ b := h.mono fun Ï‰ h => h.2
  have hX_intâ‚‚ : Integrable (fun Ï‰ â†¦ -X Ï‰ ^ 2) Î¼ :=
    (memLp_of_bounded h hX.aestronglyMeasurable 2).integrable_sq.neg
  have hX_intâ‚ : Integrable (fun Ï‰ â†¦ (a + b) * X Ï‰) Î¼ :=
    ((integrable_const (max |a| |b|)).mono' hX.aestronglyMeasurable
      (by filter_upwards [ha, hb] with Ï‰ using abs_le_max_abs_abs)).const_mul (a + b)
  have h0 : 0 â‰¤ -Î¼[X ^ 2] + (a + b) * Î¼[X] - a * b :=
    calc
      _ â‰¤ âˆ« Ï‰, (b - X Ï‰) * (X Ï‰ - a) âˆ‚Î¼ := by
        apply integral_nonneg_of_ae
        filter_upwards [ha, hb] with Ï‰ ha' hb'
        exact mul_nonneg (by linarith : 0 â‰¤ b - X Ï‰) (by linarith : 0 â‰¤ X Ï‰ - a)
      _ = âˆ« Ï‰, -X Ï‰ ^ 2 + (a + b) * X Ï‰ - a * b âˆ‚Î¼ :=
        integral_congr_ae <| ae_of_all Î¼ fun Ï‰ â†¦ by ring
      _ = âˆ« Ï‰, - X Ï‰ ^ 2 + (a + b) * X Ï‰ âˆ‚Î¼ - âˆ« _, a * b âˆ‚Î¼ :=
        integral_sub (by fun_prop) (integrable_const (a * b))
      _ = âˆ« Ï‰, - X Ï‰ ^ 2 + (a + b) * X Ï‰ âˆ‚Î¼ - a * b := by simp
      _ = - Î¼[X ^ 2] + (a + b) * Î¼[X] - a * b := by
        simp [â† integral_neg, â† integral_const_mul, integral_add hX_intâ‚‚ hX_intâ‚]
  calc
    _ â‰¤ (a + b) * Î¼[X] - a * b - Î¼[X] ^ 2 := by
      rw [variance_def' (memLp_of_bounded h hX.aestronglyMeasurable 2)]
      linarith
    _ = (b - Î¼[X]) * (Î¼[X] - a) := by ring

/-- **Popoviciu's inequality on variance**

The variance of a random variable `X` satisfying `a â‰¤ X â‰¤ b` almost everywhere is at most
`((b - a) / 2) ^ 2`. -/
lemma variance_le_sq_of_bounded [IsProbabilityMeasure Î¼] {a b : â„} {X : Î© â†’ â„}
    (h : âˆ€áµ Ï‰ âˆ‚Î¼, X Ï‰ âˆˆ Set.Icc a b) (hX : AEMeasurable X Î¼) :
    variance X Î¼ â‰¤ ((b - a) / 2) ^ 2 :=
  calc
    _ â‰¤ (b - Î¼[X]) * (Î¼[X] - a) := variance_le_sub_mul_sub h hX
    _ = ((b - a) / 2) ^ 2 - (Î¼[X] - (b + a) / 2) ^ 2 := by ring
    _ â‰¤ ((b - a) / 2) ^ 2 := sub_le_self _ (sq_nonneg _)

variable {Î¹ : Type*} [Fintype Î¹] {Î© : Î¹ â†’ Type*} {mÎ© : âˆ€ i, MeasurableSpace (Î© i)}
  {Î¼ : (i : Î¹) â†’ Measure (Î© i)}

variable {ğ’³ : Î¹ â†’ Type*} [âˆ€ i, MeasurableSpace (ğ’³ i)] {X : Î  i, Î© i â†’ ğ’³ i}

variable [âˆ€ i, IsProbabilityMeasure (Î¼ i)]

lemma iIndepFun_pi (mX : âˆ€ i, Measurable (X i)) :
    iIndepFun (fun i Ï‰ â†¦ X i (Ï‰ i)) (Measure.pi Î¼) := by
  refine @iIndepFun_iff_map_fun_eq_pi_map (Î  i, Î© i) Î¹ _ (Measure.pi Î¼) _ ğ’³ _
    (fun i x â†¦ X i (x i)) _ ?_ |>.2 ?_
  Â· exact fun i â†¦ Measurable.aemeasurable (by fun_prop)
  Â· symm
    refine Measure.pi_eq fun s hs â†¦ ?_
    rw [Measure.map_apply (by fun_prop) (MeasurableSet.univ_pi hs)]
    have : (fun (Ï‰ : Î  i, Î© i) i â†¦ X i (Ï‰ i)) â»Â¹' (Set.univ.pi s) =
        Set.univ.pi (fun i â†¦ (X i) â»Â¹' (s i)) := by ext x; simp
    rw [this, Measure.pi_pi]
    congr with i
    rw [Measure.map_apply (by fun_prop) (hs i)]
    change _ = (Measure.pi Î¼) (((X i) âˆ˜ (fun x â†¦ x i)) â»Â¹' s i)
    rw [Set.preimage_comp, â† Measure.map_apply (measurable_pi_apply i) (mX i (hs i)),
      (measurePreserving_eval i).map_eq]

lemma iIndepFun_piâ‚€ (mX : âˆ€ i, AEMeasurable (X i) (Î¼ i)) :
    iIndepFun (fun i Ï‰ â†¦ X i (Ï‰ i)) (Measure.pi Î¼) := by
  have : iIndepFun (fun i Ï‰ â†¦ (mX i).mk (X i) (Ï‰ i)) (Measure.pi Î¼) :=
    iIndepFun_pi fun i â†¦ (mX i).measurable_mk
  refine this.congr fun i â†¦ ?_
  change ((mX i).mk (X i)) âˆ˜ Function.eval i =á¶ [_] (X i) âˆ˜ Function.eval i
  apply ae_eq_comp
  Â· exact (measurable_pi_apply i).aemeasurable
  Â· rw [(measurePreserving_eval i).map_eq]
    exact (AEMeasurable.ae_eq_mk (mX i)).symm

lemma variance_pi {X : Î  i, Î© i â†’ â„} (h : âˆ€ i, MemLp (X i) 2 (Î¼ i)) :
    Var[âˆ‘ i, fun Ï‰ â†¦ X i (Ï‰ i); Measure.pi Î¼] = âˆ‘ i, Var[X i; Î¼ i] := by
  rw [IndepFun.variance_sum]
  Â· congr with i
    change Var[(X i) âˆ˜ (fun Ï‰ â†¦ Ï‰ i); Measure.pi Î¼] = _
    rw [â† variance_map, (measurePreserving_eval i).map_eq]
    Â· rw [(measurePreserving_eval i).map_eq]
      exact (h i).aemeasurable
    Â· exact Measurable.aemeasurable (by fun_prop)
  Â· exact fun i _ â†¦ (h i).comp_measurePreserving (measurePreserving_eval i)
  Â· exact fun i _ j _ hij â†¦
      (iIndepFun_piâ‚€ fun i â†¦ (h i).aemeasurable).indepFun hij

end ProbabilityTheory
