/-
Copyright (c) 2022 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Kexing Ying
-/
import Mathlib.Probability.Notation
import Mathlib.Probability.Integration
import Mathlib.MeasureTheory.Function.L2Space

/-!
# Variance of random variables

We define the variance of a real-valued random variable as `Var[X] = ùîº[(X - ùîº[X])^2]` (in the
`ProbabilityTheory` locale).

## Main definitions

* `ProbabilityTheory.evariance`: the variance of a real-valued random variable as an extended
  non-negative real.
* `ProbabilityTheory.variance`: the variance of a real-valued random variable as a real number.

## Main results

* `ProbabilityTheory.variance_le_expectation_sq`: the inequality `Var[X] ‚â§ ùîº[X^2]`.
* `ProbabilityTheory.meas_ge_le_variance_div_sq`: Chebyshev's inequality, i.e.,
      `‚Ñô {œâ | c ‚â§ |X œâ - ùîº[X]|} ‚â§ ENNReal.ofReal (Var[X] / c ^ 2)`.
* `ProbabilityTheory.meas_ge_le_evariance_div_sq`: Chebyshev's inequality formulated with
  `evariance` without requiring the random variables to be L¬≤.
* `ProbabilityTheory.IndepFun.variance_add`: the variance of the sum of two independent
  random variables is the sum of the variances.
* `ProbabilityTheory.IndepFun.variance_sum`: the variance of a finite sum of pairwise
  independent random variables is the sum of the variances.
* `ProbabilityTheory.variance_le_sub_mul_sub`: the variance of a random variable `X` satisfying
  `a ‚â§ X ‚â§ b` almost everywhere is at most `(b - ùîº X) * (ùîº X - a)`.
* `ProbabilityTheory.variance_le_sq_of_bounded`: the variance of a random variable `X` satisfying
  `a ‚â§ X ‚â§ b` almost everywhere is at most`((b - a) / 2) ^ 2`.
-/

open MeasureTheory Filter Finset

noncomputable section

open scoped MeasureTheory ProbabilityTheory ENNReal NNReal

namespace ProbabilityTheory
variable {Œ© : Type*} {mŒ© : MeasurableSpace Œ©} {X : Œ© ‚Üí ‚Ñù} {Œº : Measure Œ©}

variable (X Œº) in
-- Porting note: Consider if `evariance` or `eVariance` is better. Also,
-- consider `eVariationOn` in `Mathlib.Analysis.BoundedVariation`.
/-- The `‚Ñù‚â•0‚àû`-valued variance of a real-valued random variable defined as the Lebesgue integral of
`‚ÄñX - ùîº[X]‚Äñ^2`. -/
def evariance : ‚Ñù‚â•0‚àû := ‚à´‚Åª œâ, ‚ÄñX œâ - Œº[X]‚Äñ‚Çë ^ 2 ‚àÇŒº

variable (X Œº) in
/-- The `‚Ñù`-valued variance of a real-valued random variable defined by applying `ENNReal.toReal`
to `evariance`. -/
def variance : ‚Ñù := (evariance X Œº).toReal

/-- The `‚Ñù‚â•0‚àû`-valued variance of the real-valued random variable `X` according to the measure `Œº`.

This is defined as the Lebesgue integral of `(X - ùîº[X])^2`. -/
scoped notation "eVar[" X "; " Œº "]" => ProbabilityTheory.evariance X Œº

/-- The `‚Ñù‚â•0‚àû`-valued variance of the real-valued random variable `X` according to the volume
measure.

This is defined as the Lebesgue integral of `(X - ùîº[X])^2`. -/
scoped notation "eVar[" X "]" => eVar[X; MeasureTheory.MeasureSpace.volume]

/-- The `‚Ñù`-valued variance of the real-valued random variable `X` according to the measure `Œº`.

It is set to `0` if `X` has infinite variance. -/
scoped notation "Var[" X "; " Œº "]" => ProbabilityTheory.variance X Œº

/-- The `‚Ñù`-valued variance of the real-valued random variable `X` according to the volume measure.

It is set to `0` if `X` has infinite variance. -/
scoped notation "Var[" X "]" => Var[X; MeasureTheory.MeasureSpace.volume]

theorem evariance_lt_top [IsFiniteMeasure Œº] (hX : MemLp X 2 Œº) : evariance X Œº < ‚àû := by
  have := ENNReal.pow_lt_top (hX.sub <| memLp_const <| Œº[X]).2 (n := 2)
  rw [eLpNorm_eq_lintegral_rpow_enorm two_ne_zero ENNReal.ofNat_ne_top, ‚Üê ENNReal.rpow_two] at this
  simp only [ENNReal.toReal_ofNat, Pi.sub_apply, ENNReal.toReal_one, one_div] at this
  rw [‚Üê ENNReal.rpow_mul, inv_mul_cancel‚ÇÄ (two_ne_zero : (2 : ‚Ñù) ‚â† 0), ENNReal.rpow_one] at this
  simp_rw [ENNReal.rpow_two] at this
  exact this

lemma evariance_ne_top [IsFiniteMeasure Œº] (hX : MemLp X 2 Œº) : evariance X Œº ‚â† ‚àû :=
  (evariance_lt_top hX).ne

theorem evariance_eq_top [IsFiniteMeasure Œº] (hXm : AEStronglyMeasurable X Œº) (hX : ¬¨MemLp X 2 Œº) :
    evariance X Œº = ‚àû := by
  by_contra h
  rw [‚Üê Ne, ‚Üê lt_top_iff_ne_top] at h
  have : MemLp (fun œâ => X œâ - Œº[X]) 2 Œº := by
    refine ‚ü®by fun_prop, ?_‚ü©
    rw [eLpNorm_eq_lintegral_rpow_enorm two_ne_zero ENNReal.ofNat_ne_top]
    simp only [ENNReal.toReal_ofNat, ENNReal.toReal_one, ENNReal.rpow_two, Ne]
    exact ENNReal.rpow_lt_top_of_nonneg (by linarith) h.ne
  refine hX ?_
  convert this.add (memLp_const Œº[X])
  ext œâ
  rw [Pi.add_apply, sub_add_cancel]

theorem evariance_lt_top_iff_memLp [IsFiniteMeasure Œº] (hX : AEStronglyMeasurable X Œº) :
    evariance X Œº < ‚àû ‚Üî MemLp X 2 Œº where
  mp := by contrapose!; rw [top_le_iff]; exact evariance_eq_top hX
  mpr := evariance_lt_top

@[deprecated (since := "2025-02-21")]
alias evariance_lt_top_iff_mem‚Ñíp := evariance_lt_top_iff_memLp

lemma evariance_eq_top_iff [IsFiniteMeasure Œº] (hX : AEStronglyMeasurable X Œº) :
    evariance X Œº = ‚àû ‚Üî ¬¨ MemLp X 2 Œº := by simp [‚Üê evariance_lt_top_iff_memLp hX]

lemma variance_of_not_memLp [IsFiniteMeasure Œº] (hX : AEStronglyMeasurable X Œº)
    (hX_not : ¬¨ MemLp X 2 Œº) :
    variance X Œº = 0 := by simp [variance, (evariance_eq_top_iff hX).mpr hX_not]

theorem ofReal_variance [IsFiniteMeasure Œº] (hX : MemLp X 2 Œº) :
    .ofReal (variance X Œº) = evariance X Œº := by
  rw [variance, ENNReal.ofReal_toReal]
  exact evariance_ne_top hX

protected alias _root_.MeasureTheory.MemLp.evariance_lt_top := evariance_lt_top
protected alias _root_.MeasureTheory.MemLp.evariance_ne_top := evariance_ne_top
protected alias _root_.MeasureTheory.MemLp.ofReal_variance_eq := ofReal_variance

@[deprecated (since := "2025-02-21")]
protected alias _root_.MeasureTheory.Mem‚Ñíp.evariance_lt_top := evariance_lt_top
@[deprecated (since := "2025-02-21")]
protected alias _root_.MeasureTheory.Mem‚Ñíp.evariance_ne_top := evariance_ne_top
@[deprecated (since := "2025-02-21")]
protected alias _root_.MeasureTheory.Mem‚Ñíp.ofReal_variance_eq := ofReal_variance

variable (X Œº) in
theorem evariance_eq_lintegral_ofReal :
    evariance X Œº = ‚à´‚Åª œâ, ENNReal.ofReal ((X œâ - Œº[X]) ^ 2) ‚àÇŒº := by
  simp [evariance, ‚Üê enorm_pow, Real.enorm_of_nonneg (sq_nonneg _)]

lemma variance_eq_integral (hX : AEMeasurable X Œº) : Var[X; Œº] = ‚à´ œâ, (X œâ - Œº[X]) ^ 2 ‚àÇŒº := by
  simp [variance, evariance, toReal_enorm, ‚Üê integral_toReal ((hX.sub_const _).enorm.pow_const _) <|
    .of_forall fun _ ‚Ü¶ ENNReal.pow_lt_top enorm_lt_top]

lemma variance_of_integral_eq_zero (hX : AEMeasurable X Œº) (hXint : Œº[X] = 0) :
    variance X Œº = ‚à´ œâ, X œâ ^ 2 ‚àÇŒº := by
  simp [variance_eq_integral hX, hXint]

@[deprecated (since := "2025-01-23")]
alias _root_.MeasureTheory.Mem‚Ñíp.variance_eq := variance_eq_integral

@[deprecated (since := "2025-01-23")]
alias _root_.MeasureTheory.Mem‚Ñíp.variance_eq_of_integral_eq_zero := variance_of_integral_eq_zero

@[simp]
theorem evariance_zero : evariance 0 Œº = 0 := by simp [evariance]

theorem evariance_eq_zero_iff (hX : AEMeasurable X Œº) :
    evariance X Œº = 0 ‚Üî X =·µê[Œº] fun _ => Œº[X] := by
  simp [evariance, lintegral_eq_zero_iff' ((hX.sub_const _).enorm.pow_const _), EventuallyEq,
    sub_eq_zero]

theorem evariance_mul (c : ‚Ñù) (X : Œ© ‚Üí ‚Ñù) (Œº : Measure Œ©) :
    evariance (fun œâ => c * X œâ) Œº = ENNReal.ofReal (c ^ 2) * evariance X Œº := by
  rw [evariance, evariance, ‚Üê lintegral_const_mul' _ _ ENNReal.ofReal_lt_top.ne]
  congr with œâ
  rw [integral_const_mul, ‚Üê mul_sub, enorm_mul, mul_pow, ‚Üê enorm_pow,
    Real.enorm_of_nonneg (sq_nonneg _)]

@[simp]
theorem variance_zero (Œº : Measure Œ©) : variance 0 Œº = 0 := by
  simp only [variance, evariance_zero, ENNReal.toReal_zero]

theorem variance_nonneg (X : Œ© ‚Üí ‚Ñù) (Œº : Measure Œ©) : 0 ‚â§ variance X Œº :=
  ENNReal.toReal_nonneg

theorem variance_mul (c : ‚Ñù) (X : Œ© ‚Üí ‚Ñù) (Œº : Measure Œ©) :
    variance (fun œâ => c * X œâ) Œº = c ^ 2 * variance X Œº := by
  rw [variance, evariance_mul, ENNReal.toReal_mul, ENNReal.toReal_ofReal (sq_nonneg _)]
  rfl

theorem variance_smul (c : ‚Ñù) (X : Œ© ‚Üí ‚Ñù) (Œº : Measure Œ©) :
    variance (c ‚Ä¢ X) Œº = c ^ 2 * variance X Œº :=
  variance_mul c X Œº

theorem variance_smul' {A : Type*} [CommSemiring A] [Algebra A ‚Ñù] (c : A) (X : Œ© ‚Üí ‚Ñù)
    (Œº : Measure Œ©) : variance (c ‚Ä¢ X) Œº = c ^ 2 ‚Ä¢ variance X Œº := by
  convert variance_smul (algebraMap A ‚Ñù c) X Œº using 1
  ¬∑ congr; simp only [algebraMap_smul]
  ¬∑ simp only [Algebra.smul_def, map_pow]

theorem variance_def' [IsProbabilityMeasure Œº] {X : Œ© ‚Üí ‚Ñù} (hX : MemLp X 2 Œº) :
    variance X Œº = Œº[X ^ 2] - Œº[X] ^ 2 := by
  simp only [variance_eq_integral hX.aestronglyMeasurable.aemeasurable, sub_sq']
  rw [integral_sub, integral_add]; rotate_left
  ¬∑ exact hX.integrable_sq
  ¬∑ apply integrable_const
  ¬∑ apply hX.integrable_sq.add
    apply integrable_const
  ¬∑ exact ((hX.integrable one_le_two).const_mul 2).mul_const' _
  simp only [integral_const, measureReal_univ_eq_one, smul_eq_mul, one_mul, integral_mul_const,
    integral_const_mul, Pi.pow_apply]
  ring

lemma variance_add_const [IsProbabilityMeasure Œº] (hX : AEStronglyMeasurable X Œº) (c : ‚Ñù) :
    Var[fun œâ ‚Ü¶ X œâ + c; Œº] = Var[X; Œº] := by
  by_cases hX_Lp : MemLp X 2 Œº
  ¬∑ have hX_int : Integrable X Œº := hX_Lp.integrable one_le_two
    rw [variance_eq_integral (hX.add_const _).aemeasurable,
      integral_add hX_int (by fun_prop), integral_const, variance_eq_integral hX.aemeasurable]
    simp
  ¬∑ rw [variance_of_not_memLp (hX.add_const _), variance_of_not_memLp hX hX_Lp]
    refine fun h_memLp ‚Ü¶ hX_Lp ?_
    have : X = fun œâ ‚Ü¶ X œâ + c - c := by ext; ring
    rw [this]
    exact h_memLp.sub (memLp_const c)

lemma variance_const_add [IsProbabilityMeasure Œº] (hX : AEStronglyMeasurable X Œº) (c : ‚Ñù) :
    Var[fun œâ ‚Ü¶ c + X œâ; Œº] = Var[X; Œº] := by
  simp_rw [add_comm c, variance_add_const hX c]

lemma variance_fun_neg : Var[fun œâ ‚Ü¶ -X œâ; Œº] = Var[X; Œº] := by
  convert variance_mul (-1) X Œº
  ¬∑ ext; ring
  ¬∑ simp

lemma variance_neg : Var[-X; Œº] = Var[X; Œº] := variance_fun_neg

lemma variance_sub_const [IsProbabilityMeasure Œº] (hX : AEStronglyMeasurable X Œº) (c : ‚Ñù) :
    Var[fun œâ ‚Ü¶ X œâ - c; Œº] = Var[X; Œº] := by
  simp_rw [sub_eq_add_neg, variance_add_const hX (-c)]

lemma variance_const_sub [IsProbabilityMeasure Œº] (hX : AEStronglyMeasurable X Œº) (c : ‚Ñù) :
    Var[fun œâ ‚Ü¶ c - X œâ; Œº] = Var[X; Œº] := by
  simp_rw [sub_eq_add_neg]
  rw [variance_const_add (by fun_prop) c, variance_fun_neg]

@[simp]
lemma variance_dirac [MeasurableSingletonClass Œ©] (x : Œ©) : Var[X; Measure.dirac x] = 0 := by
  rw [variance_eq_integral]
  ¬∑ simp
  ¬∑ exact aemeasurable_dirac

lemma variance_map {Œ©' : Type*} {mŒ©' : MeasurableSpace Œ©'} {Œº : Measure Œ©'}
    {Y : Œ©' ‚Üí Œ©} (hX : AEMeasurable X (Œº.map Y)) (hY : AEMeasurable Y Œº) :
    Var[X; Œº.map Y] = Var[X ‚àò Y; Œº] := by
  rw [variance_eq_integral hX, integral_map hY, variance_eq_integral (hX.comp_aemeasurable hY),
    integral_map hY]
  ¬∑ congr
  ¬∑ exact hX.aestronglyMeasurable
  ¬∑ refine AEStronglyMeasurable.pow ?_ _
    exact AEMeasurable.aestronglyMeasurable (by fun_prop)

lemma variance_id_map (hX : AEMeasurable X Œº) : Var[id; Œº.map X] = Var[X; Œº] := by
  simp [variance_map measurable_id.aemeasurable hX]

theorem variance_le_expectation_sq [IsProbabilityMeasure Œº] {X : Œ© ‚Üí ‚Ñù}
    (hm : AEStronglyMeasurable X Œº) : variance X Œº ‚â§ Œº[X ^ 2] := by
  by_cases hX : MemLp X 2 Œº
  ¬∑ rw [variance_def' hX]
    simp only [sq_nonneg, sub_le_self_iff]
  rw [variance, evariance_eq_lintegral_ofReal, ‚Üê integral_eq_lintegral_of_nonneg_ae]
  ¬∑ by_cases hint : Integrable X Œº; swap
    ¬∑ simp only [integral_undef hint, Pi.pow_apply, Pi.sub_apply, sub_zero]
      exact le_rfl
    ¬∑ rw [integral_undef]
      ¬∑ exact integral_nonneg fun a => sq_nonneg _
      intro h
      have A : MemLp (X - fun œâ : Œ© => Œº[X]) 2 Œº :=
        (memLp_two_iff_integrable_sq (by fun_prop)).2 h
      have B : MemLp (fun _ : Œ© => Œº[X]) 2 Œº := memLp_const _
      apply hX
      convert A.add B
      simp
  ¬∑ exact Eventually.of_forall fun x => sq_nonneg _
  ¬∑ exact (AEMeasurable.pow_const (hm.aemeasurable.sub_const _) _).aestronglyMeasurable

theorem evariance_def' [IsProbabilityMeasure Œº] {X : Œ© ‚Üí ‚Ñù} (hX : AEStronglyMeasurable X Œº) :
    evariance X Œº = (‚à´‚Åª œâ, ‚ÄñX œâ‚Äñ‚Çë ^ 2 ‚àÇŒº) - ENNReal.ofReal (Œº[X] ^ 2) := by
  by_cases h‚Ñí : MemLp X 2 Œº
  ¬∑ rw [‚Üê ofReal_variance h‚Ñí, variance_def' h‚Ñí, ENNReal.ofReal_sub _ (sq_nonneg _)]
    congr
    simp_rw [‚Üê enorm_pow, enorm]
    rw [lintegral_coe_eq_integral]
    ¬∑ simp
    ¬∑ simpa using h‚Ñí.abs.integrable_sq
  ¬∑ symm
    rw [evariance_eq_top hX h‚Ñí, ENNReal.sub_eq_top_iff]
    refine ‚ü®?_, ENNReal.ofReal_ne_top‚ü©
    rw [MemLp, not_and] at h‚Ñí
    specialize h‚Ñí hX
    simp only [eLpNorm_eq_lintegral_rpow_enorm two_ne_zero ENNReal.ofNat_ne_top, not_lt, top_le_iff,
      ENNReal.toReal_ofNat, one_div, ENNReal.rpow_eq_top_iff, inv_lt_zero, inv_pos, and_true,
      or_iff_not_imp_left, not_and_or, zero_lt_two] at h‚Ñí
    exact mod_cast h‚Ñí fun _ => zero_le_two

set_option linter.deprecated false in
/-- **Chebyshev's inequality** for `‚Ñù‚â•0‚àû`-valued variance. -/
theorem meas_ge_le_evariance_div_sq {X : Œ© ‚Üí ‚Ñù} (hX : AEStronglyMeasurable X Œº) {c : ‚Ñù‚â•0}
    (hc : c ‚â† 0) : Œº {œâ | ‚Üëc ‚â§ |X œâ - Œº[X]|} ‚â§ evariance X Œº / c ^ 2 := by
  have A : (c : ‚Ñù‚â•0‚àû) ‚â† 0 := by rwa [Ne, ENNReal.coe_eq_zero]
  have B : AEStronglyMeasurable (fun _ : Œ© => Œº[X]) Œº := aestronglyMeasurable_const
  convert meas_ge_le_mul_pow_eLpNorm Œº two_ne_zero ENNReal.ofNat_ne_top (hX.sub B) A using 1
  ¬∑ congr
    simp only [Pi.sub_apply, ENNReal.coe_le_coe, ‚Üê Real.norm_eq_abs, ‚Üê coe_nnnorm,
      NNReal.coe_le_coe, ENNReal.ofReal_coe_nnreal]
  ¬∑ rw [eLpNorm_eq_lintegral_rpow_enorm two_ne_zero ENNReal.ofNat_ne_top]
    simp only [show ENNReal.ofNNReal (c ^ 2) = (ENNReal.ofNNReal c) ^ 2 by norm_cast,
      ENNReal.toReal_ofNat, one_div, Pi.sub_apply]
    rw [div_eq_mul_inv, ENNReal.inv_pow, mul_comm, ENNReal.rpow_two]
    congr
    simp_rw [‚Üê ENNReal.rpow_mul, inv_mul_cancel‚ÇÄ (two_ne_zero : (2 : ‚Ñù) ‚â† 0), ENNReal.rpow_two,
      ENNReal.rpow_one, evariance]


/-- **Chebyshev's inequality**: one can control the deviation probability of a real random variable
from its expectation in terms of the variance. -/
theorem meas_ge_le_variance_div_sq [IsFiniteMeasure Œº] {X : Œ© ‚Üí ‚Ñù} (hX : MemLp X 2 Œº) {c : ‚Ñù}
    (hc : 0 < c) : Œº {œâ | c ‚â§ |X œâ - Œº[X]|} ‚â§ ENNReal.ofReal (variance X Œº / c ^ 2) := by
  rw [ENNReal.ofReal_div_of_pos (sq_pos_of_ne_zero hc.ne.symm), hX.ofReal_variance_eq]
  convert @meas_ge_le_evariance_div_sq _ _ _ _ hX.1 c.toNNReal (by simp [hc]) using 1
  ¬∑ simp only [Real.coe_toNNReal', max_le_iff, abs_nonneg, and_true]
  ¬∑ rw [ENNReal.ofReal_pow hc.le]
    rfl

-- Porting note: supplied `MeasurableSpace Œ©` argument of `h` by unification
/-- The variance of the sum of two independent random variables is the sum of the variances. -/
theorem IndepFun.variance_add [IsProbabilityMeasure Œº] {X Y : Œ© ‚Üí ‚Ñù} (hX : MemLp X 2 Œº)
    (hY : MemLp Y 2 Œº) (h : IndepFun X Y Œº) : variance (X + Y) Œº = variance X Œº + variance Y Œº :=
  calc
    variance (X + Y) Œº = Œº[fun a => X a ^ 2 + Y a ^ 2 + 2 * X a * Y a] - Œº[X + Y] ^ 2 := by
      simp [variance_def' (hX.add hY), add_sq']
    _ = Œº[X ^ 2] + Œº[Y ^ 2] + (2 : ‚Ñù) * Œº[X * Y] - (Œº[X] + Œº[Y]) ^ 2 := by
      simp only [Pi.add_apply, Pi.pow_apply, Pi.mul_apply, mul_assoc]
      rw [integral_add, integral_add, integral_add, integral_const_mul]
      ¬∑ exact hX.integrable one_le_two
      ¬∑ exact hY.integrable one_le_two
      ¬∑ exact hX.integrable_sq
      ¬∑ exact hY.integrable_sq
      ¬∑ exact hX.integrable_sq.add hY.integrable_sq
      ¬∑ apply Integrable.const_mul
        exact h.integrable_mul (hX.integrable one_le_two) (hY.integrable one_le_two)
    _ = Œº[X ^ 2] + Œº[Y ^ 2] + 2 * (Œº[X] * Œº[Y]) - (Œº[X] + Œº[Y]) ^ 2 := by
      congr
      exact h.integral_mul_of_integrable (hX.integrable one_le_two) (hY.integrable one_le_two)
    _ = variance X Œº + variance Y Œº := by simp only [variance_def', hX, hY, Pi.pow_apply]; ring

-- Porting note: supplied `MeasurableSpace Œ©` argument of `hs`, `h` by unification
/-- The variance of a finite sum of pairwise independent random variables is the sum of the
variances. -/
theorem IndepFun.variance_sum [IsProbabilityMeasure Œº] {Œπ : Type*} {X : Œπ ‚Üí Œ© ‚Üí ‚Ñù}
    {s : Finset Œπ} (hs : ‚àÄ i ‚àà s, MemLp (X i) 2 Œº)
    (h : Set.Pairwise ‚Üës fun i j => IndepFun (X i) (X j) Œº) :
    variance (‚àë i ‚àà s, X i) Œº = ‚àë i ‚àà s, variance (X i) Œº := by
  classical
  induction s using Finset.induction_on with
  | empty => simp only [Finset.sum_empty, variance_zero]
  | insert k s ks IH =>
  rw [variance_def' (memLp_finset_sum' _ hs), sum_insert ks, sum_insert ks]
  simp only [add_sq']
  calc
    Œº[(X k ^ 2 + (‚àë i ‚àà s, X i) ^ 2 + 2 * X k * ‚àë i ‚àà s, X i : Œ© ‚Üí ‚Ñù)] - Œº[X k + ‚àë i ‚àà s, X i] ^ 2 =
        Œº[X k ^ 2] + Œº[(‚àë i ‚àà s, X i) ^ 2] + Œº[2 * X k * ‚àë i ‚àà s, X i] -
          (Œº[X k] + Œº[‚àë i ‚àà s, X i]) ^ 2 := by
      rw [integral_add', integral_add', integral_add']
      ¬∑ exact MemLp.integrable one_le_two (hs _ (mem_insert_self _ _))
      ¬∑ apply integrable_finset_sum' _ fun i hi => ?_
        exact MemLp.integrable one_le_two (hs _ (mem_insert_of_mem hi))
      ¬∑ exact MemLp.integrable_sq (hs _ (mem_insert_self _ _))
      ¬∑ apply MemLp.integrable_sq
        exact memLp_finset_sum' _ fun i hi => hs _ (mem_insert_of_mem hi)
      ¬∑ apply Integrable.add
        ¬∑ exact MemLp.integrable_sq (hs _ (mem_insert_self _ _))
        ¬∑ apply MemLp.integrable_sq
          exact memLp_finset_sum' _ fun i hi => hs _ (mem_insert_of_mem hi)
      ¬∑ rw [mul_assoc]
        apply Integrable.const_mul _ (2 : ‚Ñù)
        rw [mul_sum, sum_fn]
        apply integrable_finset_sum _ fun i hi => ?_
        apply IndepFun.integrable_mul _ (MemLp.integrable one_le_two (hs _ (mem_insert_self _ _)))
          (MemLp.integrable one_le_two (hs _ (mem_insert_of_mem hi)))
        apply h (mem_insert_self _ _) (mem_insert_of_mem hi)
        exact fun hki => ks (hki.symm ‚ñ∏ hi)
    _ = variance (X k) Œº + variance (‚àë i ‚àà s, X i) Œº +
        (Œº[2 * X k * ‚àë i ‚àà s, X i] - 2 * Œº[X k] * Œº[‚àë i ‚àà s, X i]) := by
      rw [variance_def' (hs _ (mem_insert_self _ _)),
        variance_def' (memLp_finset_sum' _ fun i hi => hs _ (mem_insert_of_mem hi))]
      ring
    _ = variance (X k) Œº + variance (‚àë i ‚àà s, X i) Œº := by
      simp_rw [Pi.mul_apply, Pi.ofNat_apply, sum_apply, mul_sum, mul_assoc,
        add_eq_left]
      rw [integral_finset_sum s fun i hi => ?_]; swap
      ¬∑ apply Integrable.const_mul _ (2 : ‚Ñù)
        apply IndepFun.integrable_mul _ (MemLp.integrable one_le_two (hs _ (mem_insert_self _ _)))
          (MemLp.integrable one_le_two (hs _ (mem_insert_of_mem hi)))
        apply h (mem_insert_self _ _) (mem_insert_of_mem hi)
        exact fun hki => ks (hki.symm ‚ñ∏ hi)
      rw [integral_finset_sum s fun i hi =>
          MemLp.integrable one_le_two (hs _ (mem_insert_of_mem hi)),
        mul_sum, mul_sum, ‚Üê sum_sub_distrib]
      apply Finset.sum_eq_zero fun i hi => ?_
      rw [integral_const_mul, IndepFun.integral_mul', sub_self]
      ¬∑ apply h (mem_insert_self _ _) (mem_insert_of_mem hi)
        exact fun hki => ks (hki.symm ‚ñ∏ hi)
      ¬∑ exact MemLp.aestronglyMeasurable (hs _ (mem_insert_self _ _))
      ¬∑ exact MemLp.aestronglyMeasurable (hs _ (mem_insert_of_mem hi))
    _ = variance (X k) Œº + ‚àë i ‚àà s, variance (X i) Œº := by
      rw [IH (fun i hi => hs i (mem_insert_of_mem hi))
          (h.mono (by simp only [coe_insert, Set.subset_insert]))]

/-- **The Bhatia-Davis inequality on variance**

The variance of a random variable `X` satisfying `a ‚â§ X ‚â§ b` almost everywhere is at most
`(b - ùîº X) * (ùîº X - a)`. -/
lemma variance_le_sub_mul_sub [IsProbabilityMeasure Œº] {a b : ‚Ñù} {X : Œ© ‚Üí ‚Ñù}
    (h : ‚àÄ·µê œâ ‚àÇŒº, X œâ ‚àà Set.Icc a b) (hX : AEMeasurable X Œº) :
    variance X Œº ‚â§ (b - Œº[X]) * (Œº[X] - a) := by
  have ha : ‚àÄ·µê œâ ‚àÇŒº, a ‚â§ X œâ := h.mono fun œâ h => h.1
  have hb : ‚àÄ·µê œâ ‚àÇŒº, X œâ ‚â§ b := h.mono fun œâ h => h.2
  have hX_int‚ÇÇ : Integrable (fun œâ ‚Ü¶ -X œâ ^ 2) Œº :=
    (memLp_of_bounded h hX.aestronglyMeasurable 2).integrable_sq.neg
  have hX_int‚ÇÅ : Integrable (fun œâ ‚Ü¶ (a + b) * X œâ) Œº :=
    ((integrable_const (max |a| |b|)).mono' hX.aestronglyMeasurable
      (by filter_upwards [ha, hb] with œâ using abs_le_max_abs_abs)).const_mul (a + b)
  have h0 : 0 ‚â§ - Œº[X ^ 2] + (a + b) * Œº[X] - a * b :=
    calc
      _ ‚â§ ‚à´ œâ, (b - X œâ) * (X œâ - a) ‚àÇŒº := by
        apply integral_nonneg_of_ae
        filter_upwards [ha, hb] with œâ ha' hb'
        exact mul_nonneg (by linarith : 0 ‚â§ b - X œâ) (by linarith : 0 ‚â§ X œâ - a)
      _ = ‚à´ œâ, - X œâ ^ 2 + (a + b) * X œâ - a * b ‚àÇŒº :=
        integral_congr_ae <| ae_of_all Œº fun œâ ‚Ü¶ by ring
      _ = ‚à´ œâ, - X œâ ^ 2 + (a + b) * X œâ ‚àÇŒº - ‚à´ _, a * b ‚àÇŒº :=
        integral_sub (by fun_prop) (integrable_const (a * b))
      _ = ‚à´ œâ, - X œâ ^ 2 + (a + b) * X œâ ‚àÇŒº - a * b := by simp
      _ = - Œº[X ^ 2] + (a + b) * Œº[X] - a * b := by
        simp [‚Üê integral_neg, ‚Üê integral_const_mul, integral_add hX_int‚ÇÇ hX_int‚ÇÅ]
  calc
    _ ‚â§ (a + b) * Œº[X] - a * b - Œº[X] ^ 2 := by
      rw [variance_def' (memLp_of_bounded h hX.aestronglyMeasurable 2)]
      linarith
    _ = (b - Œº[X]) * (Œº[X] - a) := by ring

/-- **Popoviciu's inequality on variance**

The variance of a random variable `X` satisfying `a ‚â§ X ‚â§ b` almost everywhere is at most
`((b - a) / 2) ^ 2`. -/
lemma variance_le_sq_of_bounded [IsProbabilityMeasure Œº] {a b : ‚Ñù} {X : Œ© ‚Üí ‚Ñù}
    (h : ‚àÄ·µê œâ ‚àÇŒº, X œâ ‚àà Set.Icc a b) (hX : AEMeasurable X Œº) :
    variance X Œº ‚â§ ((b - a) / 2) ^ 2 :=
  calc
    _ ‚â§ (b - Œº[X]) * (Œº[X] - a) := variance_le_sub_mul_sub h hX
    _ = ((b - a) / 2) ^ 2 - (Œº[X] - (b + a) / 2) ^ 2 := by ring
    _ ‚â§ ((b - a) / 2) ^ 2 := sub_le_self _ (sq_nonneg _)

end ProbabilityTheory
