/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Probability.Independence.Basic
import Mathlib.Probability.Kernel.Composition.MeasureComp
import Mathlib.Probability.Kernel.Condexp
import Mathlib.Probability.Martingale.Basic
import Mathlib.Probability.Moments.MGFAnalytic

/-!
# Sub-Gaussian random variables

This presentation of sub-Gaussian random variables is inspired by section 2.5 of
[vershynin2018high]. Let `X` be a random variable. Consider the following five properties, in which
`Káµ¢` are positive reals,
* (i) for all `t â‰¥ 0`, `â„™(|X| â‰¥ t) â‰¤ 2 * exp(-t^2 / Kâ‚^2)`,
* (ii) for all `p : â„•` with `1 â‰¤ p`, `ğ”¼[|X|^p]^(1/p) â‰¤ Kâ‚‚ sqrt(p)`,
* (iii) for all `|t| â‰¤ 1/Kâ‚ƒ`, `ğ”¼[exp (t^2 * X^2)] â‰¤ exp (Kâ‚ƒ^2 * t^2)`,
* (iv) `ğ”¼[exp(X^2 / Kâ‚„)] â‰¤ 2`,
* (v) for all `t : â„`, `ğ”¼[exp (t * X)] â‰¤ exp (Kâ‚… * t^2 / 2)`.

Properties (i) to (iv) are equivalent, in the sense that there exists a constant `C` such that
if `X` satisfies one of those properties with constant `K`, then it satisfies any other one with
constant at most `CK`.

If `ğ”¼[X] = 0` then properties (i)-(iv) are equivalent to (v) in that same sense.

The name sub-Gaussian is used by various authors to refer to any one of (i)-(v). We will say that a
random variable has sub-Gaussian moment generating function (mgf) with constant `Kâ‚…` to mean that
property (v) holds with that constant. The function `exp (Kâ‚… * t^2 / 2)` which appears in
property (v) is the mgf of a Gaussian with variance `Kâ‚…`.

TODO: implement (i)-(iv) and prove relations between those properties.

TODO TODO: adapt this text to the new implementation. Talk about kernels, conditional sub-G, sub-G.

## Main definitions

*

## Main statements

*

## References

* [R. Vershynin, *High-dimensional probability: An introduction with applications in data
science*][vershynin2018high]

-/

open MeasureTheory Real

open scoped ENNReal NNReal Topology

namespace ProbabilityTheory

variable {Î© Î©' : Type*} (m : MeasurableSpace Î©) {m1 m2 mÎ© : MeasurableSpace Î©} (hm : m â‰¤ mÎ©)
  {mÎ©' : MeasurableSpace Î©'}
  {Î¼ : Measure Î©} {Î½ : Measure Î©'} {Îº : Kernel Î©' Î©} {X : Î© â†’ â„} {c : â„â‰¥0} {Îµ : â„}

-- todo: fix measurable space arguments in Measure.bind and in Measure.snd_map_prod_mk
lemma condExpKernel_comp_trim [StandardBorelSpace Î©] [IsFiniteMeasure Î¼] :
    @Measure.bind _ _ m mÎ© (Î¼.trim hm) (condExpKernel Î¼ m) = Î¼ := by
  rw [â† Measure.snd_compProd, compProd_trim_condExpKernel, @Measure.snd_map_prod_mk]
  Â· simp
  Â· exact measurable_id'' hm

-- todo: delete?
theorem condExp_ae_eq_trim_integral_condExpKernel {F : Type*} [NormedAddCommGroup F] {f : Î© â†’ F}
    [NormedSpace â„ F] [CompleteSpace F]
    [StandardBorelSpace Î©] [IsFiniteMeasure Î¼]
    (hm : m â‰¤ mÎ©) (hf : StronglyMeasurable f) (hf_int : Integrable f Î¼) :
    Î¼[f|m] =áµ[Î¼.trim hm] fun Ï‰ â†¦ âˆ« y, f y âˆ‚condExpKernel Î¼ m Ï‰ := by
 refine StronglyMeasurable.ae_eq_trim_of_stronglyMeasurable hm ?_ ?_ ?_
 Â· exact stronglyMeasurable_condExp
 Â· exact hf.integral_condExpKernel
 Â· exact condExp_ae_eq_integral_condExpKernel hm hf_int

@[simp]
lemma prodMkLeft_comp_compProd {Î©'' : Type*} {mÎ©'' : MeasurableSpace Î©''}
    {Î· : Kernel Î© Î©''} [SFinite Î½] [IsSFiniteKernel Îº] :
    (Î·.prodMkLeft Î©') âˆ˜â‚˜ Î½ âŠ—â‚˜ Îº = Î· âˆ˜â‚˜ Îº âˆ˜â‚˜ Î½ := by
  conv_rhs => rw [â† Measure.snd_compProd (Î¼ := Î½)]
  rw [Kernel.prodMkLeft, Measure.snd, â† Measure.deterministic_comp_eq_map measurable_snd,
    Measure.comp_assoc, Kernel.comp_deterministic_eq_comap]

section Kernel

/-! ### Sub-Gaussian with respect to a kernel and a measure -/

/-- A random variable is sub-Gaussian with parameter `c` with respect to a kernel `Îº` and
a measure `Î½` if `Î½`-almost surely, for all `t : â„`, the moment generating function of `X`
with respect to `Îº` is bounded by `exp (c * t ^ 2 / 2)`. -/
structure Kernel.IsSubGaussianWith (X : Î© â†’ â„) (c : â„â‰¥0)
    (Îº : Kernel Î©' Î©) (Î½ : Measure Î©' := by volume_tac) : Prop where
  integrable_exp_mul : âˆ€ t : â„, Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) (Îº âˆ˜â‚˜ Î½)
  mgf_le : âˆ€áµ Ï‰' âˆ‚Î½, âˆ€ t : â„, mgf X (Îº Ï‰') t â‰¤ exp (c * t ^ 2 / 2)

def Kernel.IsSubGaussian (X : Î© â†’ â„) (Îº : Kernel Î©' Î©) (Î½ : Measure Î©' := by volume_tac) : Prop :=
  âˆƒ c : â„â‰¥0, Kernel.IsSubGaussianWith X c Îº Î½

namespace Kernel.IsSubGaussianWith

lemma aestronglyMeasurable (h : IsSubGaussianWith X c Îº Î½) : AEStronglyMeasurable X (Îº âˆ˜â‚˜ Î½) := by
  have h_int := h.integrable_exp_mul 1
  simp only [one_mul] at h_int
  exact (aemeasurable_of_aemeasurable_exp h_int.1.aemeasurable).aestronglyMeasurable

lemma ae_integrable_exp_mul [SFinite Î½] [IsSFiniteKernel Îº]
    (h : IsSubGaussianWith X c Îº Î½) (t : â„) :
    âˆ€áµ Ï‰' âˆ‚Î½, Integrable (fun y â†¦ exp (t * X y)) (Îº Ï‰') :=
  Measure.ae_integrable_of_integrable_comp (h.integrable_exp_mul t)

lemma ae_aestronglyMeasurable [SFinite Î½] [IsSFiniteKernel Îº] (h : IsSubGaussianWith X c Îº Î½) :
    âˆ€áµ Ï‰' âˆ‚Î½, AEStronglyMeasurable X (Îº Ï‰') := by
  have h_int := h.ae_integrable_exp_mul 1
  simp only [one_mul] at h_int
  filter_upwards [h_int] with Ï‰ h_int
  exact (aemeasurable_of_aemeasurable_exp h_int.1.aemeasurable).aestronglyMeasurable

lemma ae_forall_integrable_exp_mul [SFinite Î½] [IsSFiniteKernel Îº] (h : IsSubGaussianWith X c Îº Î½) :
    âˆ€áµ Ï‰' âˆ‚Î½, âˆ€ t : â„, Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) (Îº Ï‰') := by
  have h_int : âˆ€ n : â„¤, âˆ€áµ Ï‰' âˆ‚Î½, Integrable (fun Ï‰ â†¦ exp (n * X Ï‰)) (Îº Ï‰') :=
    fun _ â†¦ h.ae_integrable_exp_mul _
  rw [â† ae_all_iff] at h_int
  filter_upwards [h_int] with Ï‰' h_int t
  exact integrable_exp_mul_of_le_of_le (h_int _) (h_int _) (Int.floor_le t) (Int.le_ceil t)

lemma integrableExpSet_eq_univ [SFinite Î½] [IsSFiniteKernel Îº] (h : IsSubGaussianWith X c Îº Î½) :
    âˆ€áµ Ï‰' âˆ‚Î½, integrableExpSet X (Îº Ï‰') = Set.univ := by
  filter_upwards [h.ae_forall_integrable_exp_mul] with Ï‰' h_int
  ext t
  simp [h_int t, integrableExpSet]

lemma integrable_exp_mul_of_int
    (h_int : âˆ€ n : â„¤, âˆ€áµ Ï‰' âˆ‚Î½, Integrable (fun Ï‰ â†¦ exp (n * X Ï‰)) (Îº Ï‰')) :
    âˆ€áµ Ï‰' âˆ‚Î½, âˆ€ t, Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) (Îº Ï‰') := by
  rw [â† ae_all_iff] at h_int
  filter_upwards [h_int] with Ï‰' h_int t
  refine integrable_exp_mul_of_le_of_le (h_int _) (h_int _) (Int.floor_le t) (Int.le_ceil t)

protected lemma of_rat [SFinite Î½] [IsSFiniteKernel Îº]
    (h_int : âˆ€ t : â„, Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) (Îº âˆ˜â‚˜ Î½))
    (h_mgf : âˆ€ q : â„š, âˆ€áµ Ï‰' âˆ‚Î½, mgf X (Îº Ï‰') q â‰¤ exp (c * q ^ 2 / 2)) :
    Kernel.IsSubGaussianWith X c Îº Î½ where
  integrable_exp_mul := h_int
  mgf_le := by
    rw [â† ae_all_iff] at h_mgf
    have h_int : âˆ€áµ Ï‰' âˆ‚Î½, âˆ€ t, Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) (Îº Ï‰') := by
      refine integrable_exp_mul_of_int (fun n â†¦ ?_)
      exact Measure.ae_integrable_of_integrable_comp (h_int n)
    filter_upwards [h_mgf, h_int]
      with Ï‰' h_mgf h_int t
    refine Rat.denseRange_cast.induction_on t ?_ h_mgf
    refine isClosed_le ?_ (by fun_prop)
    refine continuous_mgf fun u â†¦ ?_
    exact h_int _

protected lemma memâ„’p (h : IsSubGaussianWith X c Îº Î½) (t : â„) (p : â„â‰¥0) :
    Memâ„’p (fun Ï‰ â†¦ exp (t * X Ï‰)) p (Îº âˆ˜â‚˜ Î½) := by
  by_cases hp0 : p = 0
  Â· simp only [hp0, ENNReal.coe_zero, memâ„’p_zero_iff_aestronglyMeasurable]
    exact (h.integrable_exp_mul t).1
  constructor
  Â· exact (h.integrable_exp_mul t).1
  Â· rw [eLpNorm_lt_top_iff_lintegral_rpow_enorm_lt_top]
    rotate_left
    Â· exact mod_cast hp0
    Â· simp
    simp only [ENNReal.coe_toReal]
    have h' := (h.integrable_exp_mul (p * t)).2
    rw [hasFiniteIntegral_def] at h'
    convert h' using 3 with p
    rw [enorm_eq_ofReal (by positivity), enorm_eq_ofReal (by positivity),
      ENNReal.ofReal_rpow_of_nonneg (by positivity)]
    swap; Â· simp
    rw [â† exp_mul, mul_comm, â† mul_assoc]

lemma cgf_le [SFinite Î½] [IsSFiniteKernel Îº] (h : IsSubGaussianWith X c Îº Î½) (t : â„) :
    âˆ€áµ Ï‰' âˆ‚Î½, cgf X (Îº Ï‰') t â‰¤ c * t ^ 2 / 2 := by
  filter_upwards [h.mgf_le, h.ae_forall_integrable_exp_mul] with Ï‰' h h_int
  calc cgf X (Îº Ï‰') t
  _ = log (mgf X (Îº Ï‰') t) := rfl
  _ â‰¤ log (exp (c * t ^ 2 / 2)) := by
    by_cases h0 : Îº Ï‰' = 0
    Â· simp only [h0, mgf_zero_measure, Pi.zero_apply, log_zero, log_exp]
      positivity
    gcongr
    Â· exact mgf_pos' h0 (h_int t)
    Â· exact h t
  _ â‰¤ c * t ^ 2 / 2 := by rw [log_exp]

@[simp]
lemma zero [IsFiniteMeasure Î½] [IsZeroOrMarkovKernel Îº] : IsSubGaussianWith (fun _ â†¦ 0) 0 Îº Î½ := by
  refine .of_rat ?_ ?_
  Â· simp
  Â· refine fun q â†¦ ?_
    simp only [mgf_const', mul_zero, exp_zero, mul_one, NNReal.coe_zero, zero_mul, zero_div]
    exact ae_of_all _ fun _ â†¦ toReal_prob_le_one

@[simp]
lemma zero' [IsFiniteMeasure Î½] [IsZeroOrMarkovKernel Îº] : IsSubGaussianWith 0 0 Îº Î½ := zero

lemma congr [SFinite Î½] [IsSFiniteKernel Îº] {Y : Î© â†’ â„} (h : IsSubGaussianWith X c Îº Î½)
    (h' : X =áµ[Îº âˆ˜â‚˜ Î½] Y) :
    IsSubGaussianWith Y c Îº Î½ where
  integrable_exp_mul t := by
    refine (integrable_congr ?_).mpr (h.integrable_exp_mul t)
    filter_upwards [h'] with Ï‰' hÏ‰'
    rw [hÏ‰']
  mgf_le := by
    have h'' := Measure.ae_ae_of_ae_comp h'
    filter_upwards [h.mgf_le, h''] with Ï‰' h_mgf h' t
    rw [mgf_congr (Filter.EventuallyEq.symm h')]
    exact h_mgf t

lemma _root_.ProbabilityTheory.Kernel.isSubGaussianWith_congr [SFinite Î½] [IsSFiniteKernel Îº]
    {Y : Î© â†’ â„} (h : X =áµ[Îº âˆ˜â‚˜ Î½] Y) :
    IsSubGaussianWith X c Îº Î½ â†” IsSubGaussianWith Y c Îº Î½ :=
  âŸ¨fun hX â†¦ congr hX h, fun hY â†¦ congr hY <| by filter_upwards [h] with Ï‰' hÏ‰' using hÏ‰'.symmâŸ©

lemma id_map (hX : Measurable X) :
    IsSubGaussianWith id c (Îº.map X) Î½ â†” IsSubGaussianWith X c Îº Î½ := by
  have h_map : (Îº.map X) âˆ˜â‚˜ Î½ = (Îº âˆ˜â‚˜ Î½).map X := by
    rw [â† deterministic_comp_eq_map hX, â† Measure.comp_assoc, Measure.deterministic_comp_eq_map]
  refine âŸ¨fun âŸ¨h1, h2âŸ© â†¦ âŸ¨?_, ?_âŸ©, fun âŸ¨h1, h2âŸ© â†¦ âŸ¨?_, ?_âŸ©âŸ©
  Â· intro t
    specialize h1 t
    rw [h_map] at h1
    rw [integrable_map_measure] at h1
    Â· exact h1
    Â· exact Measurable.aestronglyMeasurable <| by fun_prop
    Â· fun_prop
  Â· simpa [Kernel.map_apply _ hX, mgf_id_map hX.aemeasurable] using h2
  Â· intro t
    specialize h1 t
    rw [h_map, integrable_map_measure]
    Â· exact h1
    Â· exact Measurable.aestronglyMeasurable <| by fun_prop
    Â· fun_prop
  Â· simpa [Kernel.map_apply _ hX, mgf_id_map hX.aemeasurable] using h2

lemma measure_ge_le_exp_add [SFinite Î½] [IsFiniteKernel Îº] (h : IsSubGaussianWith X c Îº Î½) (Îµ : â„) :
    âˆ€áµ Ï‰' âˆ‚Î½, âˆ€ t, 0 â‰¤ t â†’
      (Îº Ï‰' {Ï‰ | Îµ â‰¤ X Ï‰}).toReal â‰¤ exp (- t * Îµ + c * t ^ 2 / 2) := by
  filter_upwards [h.mgf_le, h.ae_forall_integrable_exp_mul] with Ï‰' h1 h2 t ht
  calc (Îº Ï‰' {Ï‰ | Îµ â‰¤ X Ï‰}).toReal
  _ â‰¤ exp (-t * Îµ) * mgf X (Îº Ï‰') t := measure_ge_le_exp_mul_mgf Îµ ht (h2 t)
  _ â‰¤ exp (-t * Îµ + c * t ^ 2 / 2) := by
    rw [exp_add]
    gcongr
    exact h1 t

/-- Chernoff bound on the right tail of a sub-Gaussian random variable. -/
lemma measure_ge_le [SFinite Î½] [IsFiniteKernel Îº] (h : IsSubGaussianWith X c Îº Î½) {Îµ : â„}
    (hc : 0 < c) (hÎµ : 0 â‰¤ Îµ) :
    âˆ€áµ Ï‰' âˆ‚Î½, (Îº Ï‰' {Ï‰ | Îµ â‰¤ X Ï‰}).toReal â‰¤ exp (- Îµ ^ 2 / (2 * c)) := by
  filter_upwards [measure_ge_le_exp_add h Îµ] with Ï‰' h
  calc (Îº Ï‰' {Ï‰ | Îµ â‰¤ X Ï‰}).toReal
  -- choose the minimizer of the r.h.s. of `h` for `t â‰¥ 0`. That is, `t = Îµ / c`.
  _ â‰¤ exp (- (Îµ / c) * Îµ + c * (Îµ / c) ^ 2 / 2) := h (Îµ / c) (by positivity)
  _ = exp (- Îµ ^ 2 / (2 * c)) := by congr; field_simp; ring

lemma prob_ge_le [SFinite Î½] [IsMarkovKernel Îº] (h : IsSubGaussianWith X c Îº Î½) (hÎµ : 0 â‰¤ Îµ) :
    âˆ€áµ Ï‰' âˆ‚Î½, (Îº Ï‰' {Ï‰ | Îµ â‰¤ X Ï‰}).toReal â‰¤ exp (- Îµ ^ 2 / (2 * c)) := by
  by_cases hc0 : c = 0
  Â· refine ae_of_all _ fun Ï‰' â†¦ ?_
    simpa [hc0] using toReal_prob_le_one
  Â· exact h.measure_ge_le (lt_of_le_of_ne zero_le' (Ne.symm hc0)) hÎµ

section Add

variable {Î©'' : Type*} {mÎ©'' : MeasurableSpace Î©''} {Y : Î©'' â†’ â„} {cY : â„â‰¥0}
  [SFinite Î½] [IsSFiniteKernel Îº]

lemma prodMkLeft_compProd {Î· : Kernel Î© Î©''} (h : IsSubGaussianWith Y cY Î· (Îº âˆ˜â‚˜ Î½)) :
    IsSubGaussianWith Y cY (prodMkLeft Î©' Î·) (Î½ âŠ—â‚˜ Îº) := by
  constructor
  Â· convert h.integrable_exp_mul
    simp
  Â· have h2 := h.mgf_le
    simp only [prodMkLeft_apply] at h2
    rw [â† Measure.snd_compProd, Measure.snd] at h2
    refine ae_of_ae_map ?_ h2
    fun_prop

lemma integrable_exp_add_compProd {Î· : Kernel (Î©' Ã— Î©) Î©''} [IsMarkovKernel Î·]
    (hX : IsSubGaussianWith X c Îº Î½) (hY : IsSubGaussianWith Y cY Î· (Î½ âŠ—â‚˜ Îº)) (t : â„) :
    Integrable (fun Ï‰ â†¦ exp (t * (X Ï‰.1 + Y Ï‰.2))) (â‡‘(Îº âŠ—â‚– Î·) âˆ˜â‚˜ Î½) := by
  simp_rw [mul_add, exp_add]
  refine Memâ„’p.integrable_mul ?_ ?_
  Â· have h := hX.memâ„’p t 2
    simp only [ENNReal.coe_ofNat] at h
    have : Îº âˆ˜â‚˜ Î½ = ((Îº âŠ—â‚– Î·) âˆ˜â‚˜ Î½).map Prod.fst := by
      rw [Measure.map_comp _ _ measurable_fst, â† fst_eq, fst_compProd]
    rwa [this, memâ„’p_map_measure_iff h.1 measurable_fst.aemeasurable] at h
  Â· have h := hY.memâ„’p t 2
    simp only [ENNReal.coe_ofNat] at h
    rwa [Measure.comp_compProd_comm, Measure.snd,
      memâ„’p_map_measure_iff h.1 measurable_snd.aemeasurable] at h

lemma add {Î· : Kernel (Î©' Ã— Î©) Î©''} [IsMarkovKernel Î·]
    (hX : IsSubGaussianWith X c Îº Î½) (hY : IsSubGaussianWith Y cY Î· (Î½ âŠ—â‚˜ Îº)) :
    IsSubGaussianWith (fun p â†¦ X p.1 + Y p.2) (c + cY) (Îº âŠ—â‚– Î·) Î½ := by
  refine .of_rat (integrable_exp_add_compProd hX hY) ?_
  intro q
  filter_upwards [hX.mgf_le, hX.ae_integrable_exp_mul q, Measure.ae_ae_of_ae_compProd hY.mgf_le,
    Measure.ae_integrable_of_integrable_comp <| integrable_exp_add_compProd hX hY q]
    with Ï‰' hX_mgf hX_int hY_mgf h_int_mul
  calc mgf (fun p â†¦ X p.1 + Y p.2) ((Îº âŠ—â‚– Î·) Ï‰') q
  _ = âˆ« x, exp (q * X x) * âˆ« y, exp (q * Y y) âˆ‚(Î· (Ï‰', x)) âˆ‚(Îº Ï‰') := by
    simp_rw [mgf, mul_add, exp_add] at h_int_mul âŠ¢
    simp_rw [integral_compProd h_int_mul, integral_mul_left]
  _ â‰¤ âˆ« x, exp (q * X x) * exp (cY * q ^ 2 / 2) âˆ‚(Îº Ï‰') := by
    refine integral_mono_of_nonneg ?_ (hX_int.mul_const _) ?_
    Â· exact ae_of_all _ fun  Ï‰ â†¦ mul_nonneg (by positivity)
        (integral_nonneg (fun _ â†¦ by positivity))
    Â· filter_upwards [all_ae_of hY_mgf q] with Ï‰ hY_mgf
      gcongr
      exact hY_mgf
  _ â‰¤ exp (â†‘(c + cY) * q ^ 2 / 2) := by
    rw [integral_mul_right, NNReal.coe_add, add_mul, add_div, exp_add]
    gcongr
    exact hX_mgf q

lemma add' {Î· : Kernel Î© Î©''} [IsMarkovKernel Î·]
    (hX : IsSubGaussianWith X c Îº Î½) (hY : IsSubGaussianWith Y cY Î· (Îº âˆ˜â‚˜ Î½)) :
    IsSubGaussianWith (fun p â†¦ X p.1 + Y p.2) (c + cY) (Îº âŠ—â‚– prodMkLeft Î©' Î·) Î½ :=
  hX.add (prodMkLeft_compProd hY)

end Add

section Indep

lemma add_of_indepFun {Y : Î© â†’ â„} {cX cY : â„â‰¥0} [SFinite Î½] [IsSFiniteKernel Îº]
    (hX : IsSubGaussianWith X cX Îº Î½) (hY : IsSubGaussianWith Y cY Îº Î½)
    (hindep : IndepFun X Y Îº Î½) :
    IsSubGaussianWith (X + Y) (cX + cY) Îº Î½ := by
  have h_expX t : âˆƒ X', StronglyMeasurable X'
      âˆ§ âˆ€áµ Ï‰' âˆ‚Î½, (fun Ï‰ â†¦ exp (t * X Ï‰)) =á¶ [ae (Îº Ï‰')] X' := by
    obtain âŸ¨X', hX', hXX'âŸ© := hX.aestronglyMeasurable
    refine âŸ¨fun Ï‰ â†¦ exp (t * X' Ï‰), continuous_exp.comp_stronglyMeasurable (hX'.const_mul _), ?_âŸ©
    filter_upwards [Measure.ae_ae_of_ae_comp hXX'] with Ï‰' hÏ‰'
    filter_upwards [hÏ‰'] with Ï‰ hÏ‰
    rw [hÏ‰]
  have h_expY t : âˆƒ Y', StronglyMeasurable Y'
      âˆ§ âˆ€áµ Ï‰' âˆ‚Î½, (fun Ï‰ â†¦ exp (t * Y Ï‰)) =á¶ [ae (Îº Ï‰')] Y' := by
    obtain âŸ¨Y', hY', hYY'âŸ© := hY.aestronglyMeasurable
    refine âŸ¨fun Ï‰ â†¦ exp (t * Y' Ï‰), continuous_exp.comp_stronglyMeasurable (hY'.const_mul _), ?_âŸ©
    filter_upwards [Measure.ae_ae_of_ae_comp hYY'] with Ï‰' hÏ‰'
    filter_upwards [hÏ‰'] with Ï‰ hÏ‰
    rw [hÏ‰]
  refine .of_rat ?_ ?_
  Â· intro t
    simp_rw [Pi.add_apply, mul_add, exp_add]
    exact Memâ„’p.integrable_mul (hX.memâ„’p t 2) (hY.memâ„’p t 2)
  Â· intro q
    have h := hindep.mgf_add (h_expX q) (h_expY q)
    filter_upwards [h, hX.mgf_le, hY.mgf_le] with Ï‰' h hX hY
    calc mgf (X + Y) (Îº Ï‰') q
    _ = mgf X (Îº Ï‰') q * mgf Y (Îº Ï‰') q := by rw [h]
    _ â‰¤ exp (cX * q ^ 2 / 2) * exp (cY * q ^ 2 / 2) := by
      gcongr
      Â· exact mgf_nonneg
      Â· exact hX q
      Â· exact hY q
    _ = exp ((cX + cY) * q ^ 2 / 2) := by
      rw [â† exp_add]
      congr
      ring

lemma sum_of_iIndepFun {Î¹ : Type*} [IsFiniteMeasure Î½] [IsZeroOrMarkovKernel Îº]
    {X : Î¹ â†’ Î© â†’ â„} (h_indep : iIndepFun (fun _ â†¦ inferInstance) X Îº Î½) {c : Î¹ â†’ â„â‰¥0}
    (h_meas : âˆ€ i, Measurable (X i))
    {s : Finset Î¹} (h_subG : âˆ€ i âˆˆ s, IsSubGaussianWith (X i) (c i) Îº Î½) :
    IsSubGaussianWith (âˆ‘ i âˆˆ s, X i) (âˆ‘ i âˆˆ s, c i) Îº Î½ := by
  classical
  induction s using Finset.induction_on with
  | empty => simp
  | @insert i s his h =>
    rw [Finset.sum_insert his, Finset.sum_insert his]
    have h_indep' := (h_indep.indepFun_finset_sum_of_not_mem h_meas his).symm
    refine add_of_indepFun (h_subG _ (Finset.mem_insert_self _ _)) (h ?_) h_indep'
    exact fun i hi â†¦ h_subG _ (Finset.mem_insert_of_mem hi)

end Indep

end Kernel.IsSubGaussianWith

end Kernel

section Conditional

variable [StandardBorelSpace Î©] [IsFiniteMeasure Î¼]

def IsCondSubGaussianWith (X : Î© â†’ â„) (c : â„â‰¥0)
    (Î¼ : Measure Î© := by volume_tac) [IsFiniteMeasure Î¼] : Prop :=
  Kernel.IsSubGaussianWith X c (condExpKernel Î¼ m) (Î¼.trim hm)

def IsCondSubGaussian (X : Î© â†’ â„) (Î¼ : Measure Î© := by volume_tac) [IsFiniteMeasure Î¼] : Prop :=
  âˆƒ c : â„â‰¥0, IsCondSubGaussianWith m hm X c Î¼

lemma IsCondSubGaussianWith.condExp_le (h : IsCondSubGaussianWith m hm X c Î¼) (t : â„) :
    âˆ€áµ Ï‰' âˆ‚Î¼, (Î¼[fun Ï‰ â†¦ exp (t * X Ï‰) | m]) Ï‰' â‰¤ exp (c * t ^ 2 / 2) := by
  have h_eq := condExp_ae_eq_integral_condExpKernel hm (h.integrable_exp_mul t)
  simp_rw [condExpKernel_comp_trim] at h_eq
  filter_upwards [ae_of_ae_trim hm h.mgf_le, h_eq] with Ï‰' h_mgf h_eq
  rw [h_eq]
  exact h_mgf t

@[simp]
lemma IsCondSubGaussianWith.zero : IsCondSubGaussianWith m hm (fun _ â†¦ 0) 0 Î¼ :=
  Kernel.IsSubGaussianWith.zero

@[simp]
lemma IsCondSubGaussianWith.zero' : IsCondSubGaussianWith m hm 0 0 Î¼ :=
  Kernel.IsSubGaussianWith.zero'

lemma IsCondSubGaussianWith.memâ„’p (h : IsCondSubGaussianWith m hm X c Î¼) (t : â„) (p : â„â‰¥0) :
    Memâ„’p (fun Ï‰ â†¦ exp (t * X Ï‰)) p Î¼ :=
  condExpKernel_comp_trim (Î¼ := Î¼) m hm â–¸ Kernel.IsSubGaussianWith.memâ„’p h t p

lemma IsCondSubGaussianWith.integrable_exp_mul (h : IsCondSubGaussianWith m hm X c Î¼) (t : â„) :
    Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼ :=
  condExpKernel_comp_trim (Î¼ := Î¼) m hm â–¸ Kernel.IsSubGaussianWith.integrable_exp_mul h t

end Conditional

structure IsSubGaussianWith (X : Î© â†’ â„) (c : â„â‰¥0) (Î¼ : Measure Î© := by volume_tac) : Prop where
  integrable_exp_mul : âˆ€ t : â„, Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼
  mgf_le : âˆ€ t : â„, mgf X Î¼ t â‰¤ exp (c * t ^ 2 / 2)

def IsSubGaussian (X : Î© â†’ â„) (Î¼ : Measure Î© := by volume_tac) : Prop :=
  âˆƒ c : â„â‰¥0, IsSubGaussianWith X c Î¼

lemma isSubGaussianWith_iff_kernel :
    IsSubGaussianWith X c Î¼
      â†” Kernel.IsSubGaussianWith X c (Kernel.const Unit Î¼) (Measure.dirac ()) :=
  âŸ¨fun âŸ¨h1, h2âŸ© â†¦ âŸ¨by simpa, by simpaâŸ©, fun âŸ¨h1, h2âŸ© â†¦ âŸ¨by simpa using h1, by simpa using h2âŸ©âŸ©

lemma isSubGaussian_iff_kernel :
    IsSubGaussian X Î¼ â†” Kernel.IsSubGaussian X (Kernel.const Unit Î¼) (Measure.dirac ()) := by
  simp_rw [IsSubGaussian, Kernel.IsSubGaussian, isSubGaussianWith_iff_kernel]

namespace IsSubGaussianWith

lemma aestronglyMeasurable (h : IsSubGaussianWith X c Î¼) : AEStronglyMeasurable X Î¼ := by
  have h_int := h.integrable_exp_mul 1
  simp only [one_mul] at h_int
  exact (aemeasurable_of_aemeasurable_exp h_int.1.aemeasurable).aestronglyMeasurable

lemma memâ„’p (h : IsSubGaussianWith X c Î¼) (t : â„) (p : â„â‰¥0) :
    Memâ„’p (fun Ï‰ â†¦ exp (t * X Ï‰)) p Î¼ := by
  rw [isSubGaussianWith_iff_kernel] at h
  have hp := h.memâ„’p t p
  simpa using hp

lemma cgf_le [SFinite Î¼] (h : IsSubGaussianWith X c Î¼) (t : â„) : cgf X Î¼ t â‰¤ c * t ^ 2 / 2 := by
  rw [isSubGaussianWith_iff_kernel] at h
  simpa using h.cgf_le t

@[simp]
lemma zero [IsZeroOrProbabilityMeasure Î¼] : IsSubGaussianWith (fun _ â†¦ 0) 0 Î¼ := by
  simp [isSubGaussianWith_iff_kernel]

@[simp]
lemma zero' [IsZeroOrProbabilityMeasure Î¼] : IsSubGaussianWith 0 0 Î¼ := by
  simp [isSubGaussianWith_iff_kernel]

lemma id_map (hX : AEMeasurable X Î¼) :
    IsSubGaussianWith id c (Î¼.map X) â†” IsSubGaussianWith X c Î¼ := by
  refine âŸ¨fun âŸ¨h1, h2âŸ© â†¦ âŸ¨?_, ?_âŸ©, fun âŸ¨h1, h2âŸ© â†¦ âŸ¨?_, ?_âŸ©âŸ©
  Â· intro t
    specialize h1 t
    rw [integrable_map_measure] at h1
    Â· exact h1
    Â· exact Measurable.aestronglyMeasurable <| by fun_prop
    Â· fun_prop
  Â· simpa [Kernel.map_apply _, mgf_id_map hX] using h2
  Â· intro t
    specialize h1 t
    rw [integrable_map_measure]
    Â· exact h1
    Â· exact Measurable.aestronglyMeasurable <| by fun_prop
    Â· fun_prop
  Â· simpa [Kernel.map_apply _, mgf_id_map hX] using h2

/-- Chernoff bound on the right tail of a sub-Gaussian random variable. -/
lemma measure_ge_le [IsFiniteMeasure Î¼] (h : IsSubGaussianWith X c Î¼) {Îµ : â„}
    (hc : 0 < c) (hÎµ : 0 â‰¤ Îµ) :
    (Î¼ {Ï‰ | Îµ â‰¤ X Ï‰}).toReal â‰¤ exp (- Îµ ^ 2 / (2 * c)) := by
  rw [isSubGaussianWith_iff_kernel] at h
  simpa using h.measure_ge_le hc hÎµ

lemma prob_ge_le [IsProbabilityMeasure Î¼] (h : IsSubGaussianWith X c Î¼) (hÎµ : 0 â‰¤ Îµ) :
    (Î¼ {Ï‰ | Îµ â‰¤ X Ï‰}).toReal â‰¤ exp (- Îµ ^ 2 / (2 * c)) := by
  rw [isSubGaussianWith_iff_kernel] at h
  simpa using h.prob_ge_le hÎµ

lemma add_of_indepFun [SFinite Î¼] {Y : Î© â†’ â„} {cX cY : â„â‰¥0} (hX : IsSubGaussianWith X cX Î¼)
    (hY : IsSubGaussianWith Y cY Î¼) (hindep : IndepFun X Y Î¼) :
    IsSubGaussianWith (X + Y) (cX + cY) Î¼ := by
  rw [isSubGaussianWith_iff_kernel] at hX hY âŠ¢
  simpa using hX.add_of_indepFun hY hindep

lemma sum_of_iIndepFun {Î¹ : Type*} [IsZeroOrProbabilityMeasure Î¼]
    {X : Î¹ â†’ Î© â†’ â„} (h_indep : iIndepFun (fun _ â†¦ inferInstance) X Î¼) {c : Î¹ â†’ â„â‰¥0}
    (h_meas : âˆ€ i, Measurable (X i))
    {s : Finset Î¹} (h_subG : âˆ€ i âˆˆ s, IsSubGaussianWith (X i) (c i) Î¼) :
    IsSubGaussianWith (âˆ‘ i âˆˆ s, X i) (âˆ‘ i âˆˆ s, c i) Î¼ := by
  simp_rw [isSubGaussianWith_iff_kernel] at h_subG âŠ¢
  simpa using Kernel.IsSubGaussianWith.sum_of_iIndepFun h_indep h_meas h_subG

end IsSubGaussianWith

section Martingale

lemma mgf_map {Î¼ : Measure Î©'} {Y : Î©' â†’ Î©} {X : Î© â†’ â„} (hY : AEMeasurable Y Î¼) {t : â„}
    (h1 : AEStronglyMeasurable (fun Ï‰ â†¦ exp (t * X Ï‰)) (Î¼.map Y)) :
    mgf X (Î¼.map Y) t = mgf (X âˆ˜ Y) Î¼ t := by rw [mgf, integral_map hY h1]; rfl

lemma isSubGaussianWith_of_map {Î¼ : Measure Î©'} {Y : Î©' â†’ Î©} {X : Î© â†’ â„} (hY : AEMeasurable Y Î¼)
    (h : IsSubGaussianWith X c (Î¼.map Y)) :
    IsSubGaussianWith (X âˆ˜ Y) c Î¼ := by
  constructor
  Â· intro t
    have h1 := h.integrable_exp_mul t
    rwa [integrable_map_measure h1.aestronglyMeasurable (by fun_prop)] at h1
  Â· convert h.mgf_le
    ext t
    rw [mgf_map hY (h.integrable_exp_mul t).1]

lemma isSubGaussianWith_add_of_isCondSubGaussianWith' [StandardBorelSpace Î©] [IsFiniteMeasure Î¼]
    {Y : Î© â†’ â„} {cY : â„â‰¥0} (hm : m â‰¤ mÎ©)
    (hX : IsSubGaussianWith X c (Î¼.trim hm)) (hY : IsCondSubGaussianWith m hm Y cY Î¼) :
    IsSubGaussianWith (X + Y) (c + cY) Î¼ := by
  suffices IsSubGaussianWith (fun p â†¦ X p.1 + Y p.2) (c + cY)
      (@Measure.map Î© (Î© Ã— Î©) mÎ© (m.prod mÎ©) (fun Ï‰ â†¦ (id Ï‰, id Ï‰)) Î¼) by
    have h_eq : X + Y = (fun p â†¦ X p.1 + Y p.2) âˆ˜ (fun Ï‰ â†¦ (id Ï‰, id Ï‰)) := by ext; simp
    rw [h_eq]
    refine isSubGaussianWith_of_map ?_ this
    exact @Measurable.aemeasurable _ _ _ (m.prod mÎ©) _ _
      ((measurable_id'' hm).prod_mk measurable_id)
  rw [isSubGaussianWith_iff_kernel] at hX âŠ¢
  rw [IsCondSubGaussianWith] at hY
  have hY' : Kernel.IsSubGaussianWith Y cY (condExpKernel Î¼ m)
      (@Measure.bind  _ _ _ m (Measure.dirac ()) (Kernel.const Unit (Î¼.trim hm))) := by
    convert hY
    simp
  convert hX.add' hY'
  simp only [id_eq]
  ext
  rw [Kernel.const_apply, â† Measure.compProd, compProd_trim_condExpKernel]
  rfl

lemma IsSubGaussianWith.trim (hm : m â‰¤ mÎ©) (hXm : Measurable[m] X) (hX : IsSubGaussianWith X c Î¼) :
    IsSubGaussianWith X c (Î¼.trim hm) where
  integrable_exp_mul t := by
    refine (hX.integrable_exp_mul t).trim hm ?_
    exact Measurable.stronglyMeasurable <| by fun_prop
  mgf_le t := by
    rw [mgf, â† integral_trim]
    Â· exact hX.mgf_le t
    Â· exact Measurable.stronglyMeasurable <| by fun_prop

lemma isSubGaussianWith_add_of_isCondSubGaussianWith [StandardBorelSpace Î©] [IsFiniteMeasure Î¼]
    {Y : Î© â†’ â„} {cY : â„â‰¥0} (hm : m â‰¤ mÎ©) (hXm : Measurable[m] X)
    (hX : IsSubGaussianWith X c Î¼) (hY : IsCondSubGaussianWith m hm Y cY Î¼) :
    IsSubGaussianWith (X + Y) (c + cY) Î¼ :=
  isSubGaussianWith_add_of_isCondSubGaussianWith' m hm (hX.trim m hm hXm) hY

variable {Y : â„• â†’ Î© â†’ â„} {cY : â„• â†’ â„â‰¥0} {â„± : Filtration â„• mÎ©}

-- In particular, `âˆ‘ i, Y i` is a martingale.
lemma isSubGaussianWith_sum_of_isCondSubGaussianWith [StandardBorelSpace Î©]
    [IsZeroOrProbabilityMeasure Î¼] (h_adapted : Adapted â„± Y)
    (h_subG : âˆ€ i, IsCondSubGaussianWith (â„± i) (â„±.le i) (Y i) (cY i) Î¼) (n : â„•) :
    IsSubGaussianWith (fun Ï‰ â†¦ âˆ‘ i âˆˆ Finset.range n, Y i Ï‰) (âˆ‘ i âˆˆ Finset.range n, cY i) Î¼ := by
  induction n with
  | zero => simp
  | succ n hn =>
    simp_rw [Finset.sum_range_succ]
    refine isSubGaussianWith_add_of_isCondSubGaussianWith (â„± n) (â„±.le n) ?_ hn (h_subG n)
    exact Finset.measurable_sum (Finset.range n) fun m hm â†¦
      ((h_adapted m).mono (â„±.mono (Finset.mem_range_le hm))).measurable

end Martingale

end ProbabilityTheory
