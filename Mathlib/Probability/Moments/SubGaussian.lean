/-
Copyright (c) 2025 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import Mathlib.Probability.Kernel.Condexp
import Mathlib.Probability.Moments.MGFAnalytic

/-!
# Sub-Gaussian random variables

This presentation of sub-Gaussian random variables is inspired by section 2.5 of
[vershynin2018high]. Let `X` be a random variable. Consider the following five properties, in which
`K·µ¢` are positive reals,
* (i) for all `t ‚â• 0`, `‚Ñô(|X| ‚â• t) ‚â§ 2 * exp(-t^2 / K‚ÇÅ^2)`,
* (ii) for all `p : ‚Ñï` with `1 ‚â§ p`, `ùîº[|X|^p]^(1/p) ‚â§ K‚ÇÇ sqrt(p)`,
* (iii) for all `|t| ‚â§ 1/K‚ÇÉ`, `ùîº[exp (t^2 * X^2)] ‚â§ exp (K‚ÇÉ^2 * t^2)`,
* (iv) `ùîº[exp(X^2 / K‚ÇÑ)] ‚â§ 2`,
* (v) for all `t : ‚Ñù`, `ùîº[exp (t * X)] ‚â§ exp (K‚ÇÖ * t^2 / 2)`.

Properties (i) to (iv) are equivalent, in the sense that there exists a constant `C` such that
if `X` satisfies one of those properties with constant `K`, then it satisfies any other one with
constant at most `CK`.

If `ùîº[X] = 0` then properties (i)-(iv) are equivalent to (v) in that same sense.
Property (v) implies that `X` has expectation zero.

The name sub-Gaussian is used by various authors to refer to any one of (i)-(v). We will say that a
random variable has sub-Gaussian moment generating function (mgf) with constant `K‚ÇÖ` to mean that
property (v) holds with that constant. The function `exp (K‚ÇÖ * t^2 / 2)` which appears in
property (v) is the mgf of a Gaussian with variance `K‚ÇÖ`.
That property (v) is the most convenient one to work with if one wants to prove concentration
inequalities using Chernoff's method.

TODO: implement definitions for (i)-(iv) when it makes sense. For example the maximal constant `K‚ÇÑ`
such that (iv) is true is an Orlicz norm. Prove relations between those properties.

### Conditionally sub-Gaussian random variables and kernels

A related notion to sub-Gaussian random variables is that of conditionally sub-Gaussian random
variables. A random variable `X` is conditionally sub-Gaussian in the sense of (v) with respect to
a sigma-algebra `m` and a measure `Œº` if for all `t : ‚Ñù`, `exp (t * X)` is `Œº`-integrable and
the conditional mgf of `X` conditioned on `m` is almost surely bounded by `exp (c * t^2 / 2)`
for some constant `c`.

As in other parts of Mathlib's probability library (notably the independence and conditional
independence definitions), we express both sub-Gaussian and conditionally sub-Gaussian properties
as special cases of a notion of sub-Gaussianity with respect to a kernel and a measure.

## Main definitions

* `Kernel.HasSubgaussianMGF`: a random variable `X` has a sub-Gaussian moment generating function
  with parameter `c` with respect to a kernel `Œ∫` and a measure `ŒΩ` if for `ŒΩ`-almost all `œâ'`,
  for all `t : ‚Ñù`, the moment generating function of `X` with respect to `Œ∫ œâ'` is bounded by
  `exp (c * t ^ 2 / 2)`.
* `HasCondSubgaussianMGF`: a random variable `X` has a conditionally sub-Gaussian moment generating
  function with parameter `c` with respect to a sigma-algebra `m` and a measure `Œº` if for all
  `t : ‚Ñù`, `exp (t * X)` is `Œº`-integrable and the moment generating function of `X` conditioned
  on `m` is almost surely bounded by `exp (c * t ^ 2 / 2)` for all `t : ‚Ñù`.
  The actual definition uses `Kernel.HasSubgaussianMGF`: `HasCondSubgaussianMGF` is defined as
  sub-Gaussian with respect to the conditional expectation kernel for `m` and the restriction of `Œº`
  to the sigma-algebra `m`.
* `HasSubgaussianMGF`: a random variable `X` has a sub-Gaussian moment generating function
  with parameter `c` with respect to a measure `Œº` if for all `t : ‚Ñù`, `exp (t * X)`
  is `Œº`-integrable and the moment generating function of `X` is bounded by `exp (c * t ^ 2 / 2)`
  for all `t : ‚Ñù`.
  This is equivalent to `Kernel.HasSubgaussianMGF` with a constant kernel.
  See `HasSubgaussianMGF_iff_kernel`.

## Implementation notes

### Definition of `Kernel.HasSubgaussianMGF`

The definition of sub-Gaussian with respect to a kernel and a measure is the following:
```
structure Kernel.HasSubgaussianMGF (X : Œ© ‚Üí ‚Ñù) (c : ‚Ñù‚â•0)
    (Œ∫ : Kernel Œ©' Œ©) (ŒΩ : Measure Œ©' := by volume_tac) : Prop where
  integrable_exp_mul : ‚àÄ t, Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) (Œ∫ ‚àò‚Çò ŒΩ)
  mgf_le : ‚àÄ·µê œâ' ‚àÇŒΩ, ‚àÄ t, mgf X (Œ∫ œâ') t ‚â§ exp (c * t ^ 2 / 2)
```
An interesting point is that the integrability condition is not integrability of `exp (t * X)`
with respect to `Œ∫ œâ'` for `ŒΩ`-almost all `œâ'`, but integrability with respect to `Œ∫ ‚àò‚Çò ŒΩ`.
This is a stronger condition, as the weaker one did not allow to prove interesting results about
the sum of two sub-Gaussian random variables.

For the conditional case, that integrability condition reduces to integrability of `exp (t * X)`
with respect to `Œº`.

### Definition of `HasCondSubgaussianMGF`

We define `HasCondSubgaussianMGF` as a special case of `Kernel.HasSubgaussianMGF` with the
conditional expectation kernel for `m`, `condExpKernel Œº m`, and the restriction of `Œº` to `m`,
`Œº.trim hm` (where `hm` states that `m` is a sub-sigma-algebra).
Note that `condExpKernel Œº m ‚àò‚Çò Œº.trim hm = Œº`. The definition is equivalent to the two
conditions
* for all `t`, `exp (t * X)` is `Œº`-integrable,
* for `Œº.trim hm`-almost all `œâ`, for all `t`, the mgf with respect to the the conditional
  distribution `condExpKernel Œº m œâ` is bounded by `exp (c * t ^ 2 / 2)`.

For any `t`, we can write the mgf of `X` with respect to the conditional expectation kernel as
a conditional expectation, `(Œº.trim hm)`-almost surely:
`mgf X (condExpKernel Œº m ¬∑) t =·µê[Œº.trim hm] Œº[fun œâ' ‚Ü¶ exp (t * X œâ') | m]`.

## References

* [R. Vershynin, *High-dimensional probability: An introduction with applications in data
  science*][vershynin2018high]

-/

open MeasureTheory Real

open scoped ENNReal NNReal Topology

namespace ProbabilityTheory

section Kernel

variable {Œ© Œ©' : Type*} {mŒ© : MeasurableSpace Œ©} {mŒ©' : MeasurableSpace Œ©'}
  {ŒΩ : Measure Œ©'} {Œ∫ : Kernel Œ©' Œ©} {X : Œ© ‚Üí ‚Ñù} {c : ‚Ñù‚â•0}

/-! ### Sub-Gaussian with respect to a kernel and a measure -/

/-- A random variable `X` has a sub-Gaussian moment generating function with parameter `c`
with respect to a kernel `Œ∫` and a measure `ŒΩ` if for `ŒΩ`-almost all `œâ'`, for all `t : ‚Ñù`,
the moment generating function of `X` with respect to `Œ∫ œâ'` is bounded by `exp (c * t ^ 2 / 2)`.
This implies in particular that `X` has expectation 0. -/
structure Kernel.HasSubgaussianMGF (X : Œ© ‚Üí ‚Ñù) (c : ‚Ñù‚â•0)
    (Œ∫ : Kernel Œ©' Œ©) (ŒΩ : Measure Œ©' := by volume_tac) : Prop where
  integrable_exp_mul : ‚àÄ t, Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) (Œ∫ ‚àò‚Çò ŒΩ)
  mgf_le : ‚àÄ·µê œâ' ‚àÇŒΩ, ‚àÄ t, mgf X (Œ∫ œâ') t ‚â§ exp (c * t ^ 2 / 2)

namespace Kernel.HasSubgaussianMGF

section BasicProperties

lemma aestronglyMeasurable (h : HasSubgaussianMGF X c Œ∫ ŒΩ) :
    AEStronglyMeasurable X (Œ∫ ‚àò‚Çò ŒΩ) := by
  have h_int := h.integrable_exp_mul 1
  simpa using (aemeasurable_of_aemeasurable_exp h_int.1.aemeasurable).aestronglyMeasurable

lemma ae_integrable_exp_mul (h : HasSubgaussianMGF X c Œ∫ ŒΩ) (t : ‚Ñù) :
    ‚àÄ·µê œâ' ‚àÇŒΩ, Integrable (fun y ‚Ü¶ exp (t * X y)) (Œ∫ œâ') :=
  Measure.ae_integrable_of_integrable_comp (h.integrable_exp_mul t)

lemma ae_aestronglyMeasurable (h : HasSubgaussianMGF X c Œ∫ ŒΩ) :
    ‚àÄ·µê œâ' ‚àÇŒΩ, AEStronglyMeasurable X (Œ∫ œâ') := by
  have h_int := h.ae_integrable_exp_mul 1
  filter_upwards [h_int] with œâ h_int
  simpa using (aemeasurable_of_aemeasurable_exp h_int.1.aemeasurable).aestronglyMeasurable

lemma ae_forall_integrable_exp_mul (h : HasSubgaussianMGF X c Œ∫ ŒΩ) :
    ‚àÄ·µê œâ' ‚àÇŒΩ, ‚àÄ t, Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) (Œ∫ œâ') := by
  have h_int (n : ‚Ñ§) : ‚àÄ·µê œâ' ‚àÇŒΩ, Integrable (fun œâ ‚Ü¶ exp (n * X œâ)) (Œ∫ œâ') :=
    h.ae_integrable_exp_mul _
  rw [‚Üê ae_all_iff] at h_int
  filter_upwards [h_int] with œâ' h_int t
  exact integrable_exp_mul_of_le_of_le (h_int _) (h_int _) (Int.floor_le t) (Int.le_ceil t)

lemma memLp_exp_mul (h : HasSubgaussianMGF X c Œ∫ ŒΩ) (t : ‚Ñù) (p : ‚Ñù‚â•0) :
    MemLp (fun œâ ‚Ü¶ exp (t * X œâ)) p (Œ∫ ‚àò‚Çò ŒΩ) := by
  by_cases hp0 : p = 0
  ¬∑ simpa [hp0] using (h.integrable_exp_mul t).1
  constructor
  ¬∑ exact (h.integrable_exp_mul t).1
  ¬∑ rw [eLpNorm_lt_top_iff_lintegral_rpow_enorm_lt_top (mod_cast hp0) (by simp)]
    simp only [ENNReal.coe_toReal]
    have h' := (h.integrable_exp_mul (p * t)).2
    rw [hasFiniteIntegral_def] at h'
    convert h' using 3 with œâ
    rw [enorm_eq_ofReal (by positivity), enorm_eq_ofReal (by positivity),
      ENNReal.ofReal_rpow_of_nonneg (by positivity), ‚Üê exp_mul, mul_comm, ‚Üê mul_assoc]
    positivity

lemma cgf_le (h : HasSubgaussianMGF X c Œ∫ ŒΩ) :
    ‚àÄ·µê œâ' ‚àÇŒΩ, ‚àÄ t, cgf X (Œ∫ œâ') t ‚â§ c * t ^ 2 / 2 := by
  filter_upwards [h.mgf_le, h.ae_forall_integrable_exp_mul] with œâ' h h_int t
  calc cgf X (Œ∫ œâ') t
  _ = log (mgf X (Œ∫ œâ') t) := rfl
  _ ‚â§ log (exp (c * t ^ 2 / 2)) := by
    by_cases h0 : Œ∫ œâ' = 0
    ¬∑ simpa [h0] using by positivity
    gcongr
    ¬∑ exact mgf_pos' h0 (h_int t)
    ¬∑ exact h t
  _ ‚â§ c * t ^ 2 / 2 := by rw [log_exp]

lemma isFiniteMeasure (h : HasSubgaussianMGF X c Œ∫ ŒΩ) :
    ‚àÄ·µê œâ' ‚àÇŒΩ, IsFiniteMeasure (Œ∫ œâ') := by
  filter_upwards [h.ae_integrable_exp_mul 0, h.mgf_le] with œâ' h h_mgf
  simpa [integrable_const_iff] using h

lemma measure_univ_le_one (h : HasSubgaussianMGF X c Œ∫ ŒΩ) :
    ‚àÄ·µê œâ' ‚àÇŒΩ, Œ∫ œâ' Set.univ ‚â§ 1 := by
  filter_upwards [h.isFiniteMeasure, h.mgf_le] with œâ' h h_mgf
  suffices (Œ∫ œâ' Set.univ).toReal ‚â§ 1 by
    rwa [‚Üê ENNReal.ofReal_one, ENNReal.le_ofReal_iff_toReal_le (measure_ne_top _ _) zero_le_one]
  simpa [mgf] using h_mgf 0

end BasicProperties

protected lemma of_rat (h_int : ‚àÄ t : ‚Ñù, Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) (Œ∫ ‚àò‚Çò ŒΩ))
    (h_mgf : ‚àÄ q : ‚Ñö, ‚àÄ·µê œâ' ‚àÇŒΩ, mgf X (Œ∫ œâ') q ‚â§ exp (c * q ^ 2 / 2)) :
    Kernel.HasSubgaussianMGF X c Œ∫ ŒΩ where
  integrable_exp_mul := h_int
  mgf_le := by
    rw [‚Üê ae_all_iff] at h_mgf
    have h_int : ‚àÄ·µê œâ' ‚àÇŒΩ, ‚àÄ t, Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) (Œ∫ œâ') := by
      have h_int' (n : ‚Ñ§) := Measure.ae_integrable_of_integrable_comp (h_int n)
      rw [‚Üê ae_all_iff] at h_int'
      filter_upwards [h_int'] with œâ' h_int t
      exact integrable_exp_mul_of_le_of_le (h_int _) (h_int _) (Int.floor_le t) (Int.le_ceil t)
    filter_upwards [h_mgf, h_int] with œâ' h_mgf h_int t
    refine Rat.denseRange_cast.induction_on t ?_ h_mgf
    exact isClosed_le (continuous_mgf h_int) (by fun_prop)

@[simp]
lemma fun_zero [IsFiniteMeasure ŒΩ] [IsZeroOrMarkovKernel Œ∫] :
    HasSubgaussianMGF (fun _ ‚Ü¶ 0) 0 Œ∫ ŒΩ where
  integrable_exp_mul := by simp
  mgf_le := by simpa using ae_of_all _ fun _ ‚Ü¶ toReal_prob_le_one

@[simp]
lemma zero [IsFiniteMeasure ŒΩ] [IsZeroOrMarkovKernel Œ∫] : HasSubgaussianMGF 0 0 Œ∫ ŒΩ := fun_zero

lemma congr {Y : Œ© ‚Üí ‚Ñù} (h : HasSubgaussianMGF X c Œ∫ ŒΩ) (h' : X =·µê[Œ∫ ‚àò‚Çò ŒΩ] Y) :
    HasSubgaussianMGF Y c Œ∫ ŒΩ where
  integrable_exp_mul t := by
    refine (integrable_congr ?_).mpr (h.integrable_exp_mul t)
    filter_upwards [h'] with œâ hœâ using by rw [hœâ]
  mgf_le := by
    have h'' := Measure.ae_ae_of_ae_comp h'
    filter_upwards [h.mgf_le, h''] with œâ' h_mgf h' t
    rw [mgf_congr (Filter.EventuallyEq.symm h')]
    exact h_mgf t

lemma _root_.ProbabilityTheory.Kernel.HasSubgaussianMGF_congr {Y : Œ© ‚Üí ‚Ñù} (h : X =·µê[Œ∫ ‚àò‚Çò ŒΩ] Y) :
    HasSubgaussianMGF X c Œ∫ ŒΩ ‚Üî HasSubgaussianMGF Y c Œ∫ ŒΩ :=
  ‚ü®fun hX ‚Ü¶ congr hX h, fun hY ‚Ü¶ congr hY (ae_eq_symm h)‚ü©

section ChernoffBound

lemma measure_ge_le_exp_add (h : HasSubgaussianMGF X c Œ∫ ŒΩ) (Œµ : ‚Ñù) :
    ‚àÄ·µê œâ' ‚àÇŒΩ, ‚àÄ t, 0 ‚â§ t ‚Üí (Œ∫ œâ' {œâ | Œµ ‚â§ X œâ}).toReal ‚â§ exp (- t * Œµ + c * t ^ 2 / 2) := by
  filter_upwards [h.mgf_le, h.ae_forall_integrable_exp_mul, h.isFiniteMeasure] with œâ' h1 h2 _ t ht
  calc (Œ∫ œâ' {œâ | Œµ ‚â§ X œâ}).toReal
  _ ‚â§ exp (-t * Œµ) * mgf X (Œ∫ œâ') t := measure_ge_le_exp_mul_mgf Œµ ht (h2 t)
  _ ‚â§ exp (-t * Œµ + c * t ^ 2 / 2) := by
    rw [exp_add]
    gcongr
    exact h1 t

/-- Chernoff bound on the right tail of a sub-Gaussian random variable. -/
lemma measure_ge_le (h : HasSubgaussianMGF X c Œ∫ ŒΩ) {Œµ : ‚Ñù} (hŒµ : 0 ‚â§ Œµ) :
    ‚àÄ·µê œâ' ‚àÇŒΩ, (Œ∫ œâ' {œâ | Œµ ‚â§ X œâ}).toReal ‚â§ exp (- Œµ ^ 2 / (2 * c)) := by
  by_cases hc0 : c = 0
  ¬∑ filter_upwards [h.measure_univ_le_one] with œâ' h
    simp only [hc0, NNReal.coe_zero, mul_zero, div_zero, exp_zero]
    refine ENNReal.toReal_le_of_le_ofReal zero_le_one ?_
    simp only [ENNReal.ofReal_one]
    exact (measure_mono (Set.subset_univ _)).trans h
  filter_upwards [measure_ge_le_exp_add h Œµ] with œâ' h
  calc (Œ∫ œâ' {œâ | Œµ ‚â§ X œâ}).toReal
  -- choose the minimizer of the r.h.s. of `h` for `t ‚â• 0`. That is, `t = Œµ / c`.
  _ ‚â§ exp (- (Œµ / c) * Œµ + c * (Œµ / c) ^ 2 / 2) := h (Œµ / c) (by positivity)
  _ = exp (- Œµ ^ 2 / (2 * c)) := by congr; field_simp; ring

end ChernoffBound

end Kernel.HasSubgaussianMGF

end Kernel

section Conditional

/-! ### Conditionally sub-Gaussian moment generating function -/

variable {Œ© : Type*} {m mŒ© : MeasurableSpace Œ©} {hm : m ‚â§ mŒ©} [StandardBorelSpace Œ©]
  {Œº : Measure Œ©} [IsFiniteMeasure Œº] {X : Œ© ‚Üí ‚Ñù} {c : ‚Ñù‚â•0}

variable (m) (hm) in
/-- A random variable `X` has a conditionally sub-Gaussian moment generating function
with parameter `c` with respect to a sigma-algebra `m` and a measure `Œº` if for all `t : ‚Ñù`,
`exp (t * X)` is `Œº`-integrable and the moment generating function of `X` conditioned on `m` is
almost surely bounded by `exp (c * t ^ 2 / 2)` for all `t : ‚Ñù`.
This implies in particular that `X` has expectation 0.

The actual definition uses `Kernel.HasSubgaussianMGF`: `HasCondSubgaussianMGF` is defined as
sub-Gaussian with respect to the conditional expectation kernel for `m` and the restriction of `Œº`
to the sigma-algebra `m`. -/
def HasCondSubgaussianMGF (X : Œ© ‚Üí ‚Ñù) (c : ‚Ñù‚â•0)
    (Œº : Measure Œ© := by volume_tac) [IsFiniteMeasure Œº] : Prop :=
  Kernel.HasSubgaussianMGF X c (condExpKernel Œº m) (Œº.trim hm)

namespace HasCondSubgaussianMGF

lemma mgf_le (h : HasCondSubgaussianMGF m hm X c Œº) :
    ‚àÄ·µê œâ' ‚àÇ(Œº.trim hm), ‚àÄ t, mgf X (condExpKernel Œº m œâ') t ‚â§ exp (c * t ^ 2 / 2) :=
  Kernel.HasSubgaussianMGF.mgf_le h

lemma cgf_le (h : HasCondSubgaussianMGF m hm X c Œº) :
    ‚àÄ·µê œâ' ‚àÇ(Œº.trim hm), ‚àÄ t, cgf X (condExpKernel Œº m œâ') t ‚â§ c * t ^ 2 / 2 :=
  Kernel.HasSubgaussianMGF.cgf_le h

lemma ae_trim_condExp_le (h : HasCondSubgaussianMGF m hm X c Œº) (t : ‚Ñù) :
    ‚àÄ·µê œâ' ‚àÇ(Œº.trim hm), (Œº[fun œâ ‚Ü¶ exp (t * X œâ) | m]) œâ' ‚â§ exp (c * t ^ 2 / 2) := by
  have h_eq := condExp_ae_eq_trim_integral_condExpKernel hm (h.integrable_exp_mul t)
  simp_rw [condExpKernel_comp_trim] at h_eq
  filter_upwards [h.mgf_le, h_eq] with œâ' h_mgf h_eq
  rw [h_eq]
  exact h_mgf t

lemma ae_condExp_le (h : HasCondSubgaussianMGF m hm X c Œº) (t : ‚Ñù) :
    ‚àÄ·µê œâ' ‚àÇŒº, (Œº[fun œâ ‚Ü¶ exp (t * X œâ) | m]) œâ' ‚â§ exp (c * t ^ 2 / 2) :=
  ae_of_ae_trim hm (h.ae_trim_condExp_le t)

@[simp]
lemma fun_zero : HasCondSubgaussianMGF m hm (fun _ ‚Ü¶ 0) 0 Œº := Kernel.HasSubgaussianMGF.fun_zero

@[simp]
lemma zero : HasCondSubgaussianMGF m hm 0 0 Œº := Kernel.HasSubgaussianMGF.zero

lemma memLp_exp_mul (h : HasCondSubgaussianMGF m hm X c Œº) (t : ‚Ñù) (p : ‚Ñù‚â•0) :
    MemLp (fun œâ ‚Ü¶ exp (t * X œâ)) p Œº :=
  condExpKernel_comp_trim (Œº := Œº) hm ‚ñ∏ Kernel.HasSubgaussianMGF.memLp_exp_mul h t p

lemma integrable_exp_mul (h : HasCondSubgaussianMGF m hm X c Œº) (t : ‚Ñù) :
    Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) Œº :=
  condExpKernel_comp_trim (Œº := Œº) hm ‚ñ∏ Kernel.HasSubgaussianMGF.integrable_exp_mul h t

end HasCondSubgaussianMGF

end Conditional

/-! ### Sub-Gaussian moment generating function -/

variable {Œ© : Type*} {m mŒ© : MeasurableSpace Œ©} {Œº : Measure Œ©} {X : Œ© ‚Üí ‚Ñù} {c : ‚Ñù‚â•0}

/-- A random variable `X` has a sub-Gaussian moment generating function with parameter `c`
with respect to a measure `Œº` if for all `t : ‚Ñù`, `exp (t * X)` is `Œº`-integrable and
the moment generating function of `X` is bounded by `exp (c * t ^ 2 / 2)` for all `t : ‚Ñù`.
This implies in particular that `X` has expectation 0.

This is equivalent to `Kernel.HasSubgaussianMGF X c (Kernel.const Unit Œº) (Measure.dirac ())`,
as proved in `HasSubgaussianMGF_iff_kernel`.
Properties about sub-Gaussian moment generating functions should be proved first for
`Kernel.HasSubgaussianMGF` when possible. -/
structure HasSubgaussianMGF (X : Œ© ‚Üí ‚Ñù) (c : ‚Ñù‚â•0) (Œº : Measure Œ© := by volume_tac) : Prop where
  integrable_exp_mul : ‚àÄ t : ‚Ñù, Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) Œº
  mgf_le : ‚àÄ t : ‚Ñù, mgf X Œº t ‚â§ exp (c * t ^ 2 / 2)

lemma HasSubgaussianMGF_iff_kernel :
    HasSubgaussianMGF X c Œº
      ‚Üî Kernel.HasSubgaussianMGF X c (Kernel.const Unit Œº) (Measure.dirac ()) :=
  ‚ü®fun ‚ü®h1, h2‚ü© ‚Ü¶ ‚ü®by simpa, by simpa‚ü©, fun ‚ü®h1, h2‚ü© ‚Ü¶ ‚ü®by simpa using h1, by simpa using h2‚ü©‚ü©

namespace HasSubgaussianMGF

lemma aestronglyMeasurable (h : HasSubgaussianMGF X c Œº) : AEStronglyMeasurable X Œº := by
  have h_int := h.integrable_exp_mul 1
  simpa using (aemeasurable_of_aemeasurable_exp h_int.1.aemeasurable).aestronglyMeasurable

lemma memLp_exp_mul (h : HasSubgaussianMGF X c Œº) (t : ‚Ñù) (p : ‚Ñù‚â•0) :
    MemLp (fun œâ ‚Ü¶ exp (t * X œâ)) p Œº := by
  rw [HasSubgaussianMGF_iff_kernel] at h
  simpa using h.memLp_exp_mul t p

lemma cgf_le (h : HasSubgaussianMGF X c Œº) (t : ‚Ñù) : cgf X Œº t ‚â§ c * t ^ 2 / 2 := by
  rw [HasSubgaussianMGF_iff_kernel] at h
  simpa using (all_ae_of h.cgf_le t)

@[simp]
lemma fun_zero [IsZeroOrProbabilityMeasure Œº] : HasSubgaussianMGF (fun _ ‚Ü¶ 0) 0 Œº := by
  simp [HasSubgaussianMGF_iff_kernel]

@[simp]
lemma zero [IsZeroOrProbabilityMeasure Œº] : HasSubgaussianMGF 0 0 Œº := fun_zero

section ChernoffBound

/-- Chernoff bound on the right tail of a sub-Gaussian random variable. -/
lemma measure_ge_le (h : HasSubgaussianMGF X c Œº) {Œµ : ‚Ñù} (hŒµ : 0 ‚â§ Œµ) :
    (Œº {œâ | Œµ ‚â§ X œâ}).toReal ‚â§ exp (- Œµ ^ 2 / (2 * c)) := by
  rw [HasSubgaussianMGF_iff_kernel] at h
  simpa using h.measure_ge_le hŒµ

end ChernoffBound

end HasSubgaussianMGF

end ProbabilityTheory
