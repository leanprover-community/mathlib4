/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Probability.Kernel.Condexp
import Mathlib.Probability.Moments.MGFAnalytic

/-!
# Sub-Gaussian random variables

This presentation of sub-Gaussian random variables is inspired by section 2.5 of
[vershynin2018high]. Let `X` be a random variable. Consider the following five properties, in which
`Káµ¢` are positive reals,
* (i) for all `t â‰¥ 0`, `â„™(|X| â‰¥ t) â‰¤ 2 * exp(-t^2 / Kâ‚^2)`,
* (ii) for all `p : â„•` with `1 â‰¤ p`, `ğ”¼[|X|^p]^(1/p) â‰¤ Kâ‚‚ sqrt(p)`,
* (iii) for all `|t| â‰¤ 1/Kâ‚ƒ`, `ğ”¼[exp (t^2 * X^2)] â‰¤ exp (Kâ‚ƒ^2 * t^2)`,
* (iv) `ğ”¼[exp(X^2 / Kâ‚„)] â‰¤ 2`,
* (v) for all `t : â„`, `ğ”¼[exp (t * X)] â‰¤ exp (Kâ‚… * t^2 / 2)`.

Properties (i) to (iv) are equivalent, in the sense that there exists a constant `C` such that
if `X` satisfies one of those properties with constant `K`, then it satisfies any other one with
constant at most `CK`.

If `ğ”¼[X] = 0` then properties (i)-(iv) are equivalent to (v) in that same sense.

The name sub-Gaussian is used by various authors to refer to any one of (i)-(v). We will say that a
random variable has sub-Gaussian moment generating function (mgf) with constant `Kâ‚…` to mean that
property (v) holds with that constant. The function `exp (Kâ‚… * t^2 / 2)` which appears in
property (v) is the mgf of a Gaussian with variance `Kâ‚…`.
That property (v) is the most convenient one to work with if one wants to prove concentration
inequalities using Chernoff's method.

TODO: implement definitions for (i)-(iv) when it makes sense. For example the maximal constant `Kâ‚„`
such that (iv) is true is an Orlicz norm. Prove relations between those properties.

### Conditionally sub-Gaussian random variables and kernels

A related notion to sub-Gaussian random variables is that of conditionally sub-Gaussian random
variables. A random variable `X` is conditionally sub-Gaussian in the sense of (v) with respect to
a sigma-algebra `m` and a measure `Î¼` if for all `t : â„`, `exp (t * X)` is `Î¼`-integrable and
the conditional mgf of `X` conditioned on `m` is almost surely bounded by `exp (c * t^2 / 2)`
for some constant `c`.

As in other parts of Mathlib's probability library (notably the independence and conditional
independence definitions), we express both sub-Gaussian and conditionally sub-Gaussian properties
as special cases of a notion of sub-Gaussianity with respect to a kernel and a measure.

## Main definitions

* `Kernel.HasSubgaussianMGF`: a random variable `X` has a sub-Gaussian moment generating function
  with parameter `c` with respect to a kernel `Îº` and a measure `Î½` if for `Î½`-almost all `Ï‰'`,
  for all `t : â„`, the moment generating function of `X` with respect to `Îº Ï‰'` is bounded by
  `exp (c * t ^ 2 / 2)`.
* `HasCondSubgaussianMGF`: a random variable `X` has a conditionally sub-Gaussian moment generating
  function with parameter `c` with respect to a sigma-algebra `m` and a measure `Î¼` if for all
  `t : â„`, `exp (t * X)` is `Î¼`-integrable and the moment generating function of `X` contioned
  on `m` is almost surely bounded by `exp (c * t ^ 2 / 2)` for all `t : â„`.
  The actual definition uses `Kernel.HasSubgaussianMGF`: `HasCondSubgaussianMGF` is defined as
  sub-Gaussian with respect to the conditional expectation kernel for `m` and the restriction of `Î¼`
  to the sigma-algebra `m`.
* `HasSubgaussianMGF`: a random variable `X` has a sub-Gaussian moment generating function
  with parameter `c` with respect to a measure `Î¼` if for all `t : â„`, `exp (t * X)`
  is `Î¼`-integrable and the moment generating function of `X` is bounded by `exp (c * t ^ 2 / 2)`
  for all `t : â„`.
  This is equivalent to `Kernel.HasSubgaussianMGF` with a constant kernel.
  See `HasSubgaussianMGF_iff_kernel`.

## Implementation notes

The definition of sub-Gaussian with respect to a kernel and a measure is the following:
```
structure Kernel.HasSubgaussianMGF (X : Î© â†’ â„) (c : â„â‰¥0)
    (Îº : Kernel Î©' Î©) (Î½ : Measure Î©' := by volume_tac) : Prop where
  integrable_exp_mul : âˆ€ t, Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) (Îº âˆ˜â‚˜ Î½)
  mgf_le : âˆ€áµ Ï‰' âˆ‚Î½, âˆ€ t, mgf X (Îº Ï‰') t â‰¤ exp (c * t ^ 2 / 2)
```
An interesting point is that the integrability condition is not integrability of `exp (t * X)`
with respect to `Îº Ï‰'` for `Î½`-almost all `Ï‰'`, but integrability with respect to `Îº âˆ˜â‚˜ Î½`.
This is a stronger condition, as the weaker one did not allow to prove interesting results about
the sum of two sub-Gaussian random variables.

Fo the conditional case, that integrability condition reduces to integrability of `exp (t * X)`
with respect to `Î¼`.

## References

* [R. Vershynin, *High-dimensional probability: An introduction with applications in data
  science*][vershynin2018high]

-/

open MeasureTheory Real

open scoped ENNReal NNReal Topology

namespace ProbabilityTheory

section Kernel

variable {Î© Î©' : Type*} {mÎ© : MeasurableSpace Î©} {mÎ©' : MeasurableSpace Î©'}
  {Î½ : Measure Î©'} {Îº : Kernel Î©' Î©} {X : Î© â†’ â„} {c : â„â‰¥0}

/-! ### Sub-Gaussian with respect to a kernel and a measure -/

/-- A random variable `X` has a sub-Gaussian moment generating function with parameter `c`
with respect to a kernel `Îº` and a measure `Î½` if for `Î½`-almost all `Ï‰'`, for all `t : â„`,
the moment generating function of `X` with respect to `Îº Ï‰'` is bounded by `exp (c * t ^ 2 / 2)`. -/
structure Kernel.HasSubgaussianMGF (X : Î© â†’ â„) (c : â„â‰¥0)
    (Îº : Kernel Î©' Î©) (Î½ : Measure Î©' := by volume_tac) : Prop where
  integrable_exp_mul : âˆ€ t, Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) (Îº âˆ˜â‚˜ Î½)
  mgf_le : âˆ€áµ Ï‰' âˆ‚Î½, âˆ€ t, mgf X (Îº Ï‰') t â‰¤ exp (c * t ^ 2 / 2)

namespace Kernel.HasSubgaussianMGF

section BasicProperties

lemma aestronglyMeasurable (h : HasSubgaussianMGF X c Îº Î½) :
    AEStronglyMeasurable X (Îº âˆ˜â‚˜ Î½) := by
  have h_int := h.integrable_exp_mul 1
  simpa using (aemeasurable_of_aemeasurable_exp h_int.1.aemeasurable).aestronglyMeasurable

lemma ae_integrable_exp_mul (h : HasSubgaussianMGF X c Îº Î½) (t : â„) :
    âˆ€áµ Ï‰' âˆ‚Î½, Integrable (fun y â†¦ exp (t * X y)) (Îº Ï‰') :=
  Measure.ae_integrable_of_integrable_comp (h.integrable_exp_mul t)

lemma ae_aestronglyMeasurable (h : HasSubgaussianMGF X c Îº Î½) :
    âˆ€áµ Ï‰' âˆ‚Î½, AEStronglyMeasurable X (Îº Ï‰') := by
  have h_int := h.ae_integrable_exp_mul 1
  filter_upwards [h_int] with Ï‰ h_int
  simpa using (aemeasurable_of_aemeasurable_exp h_int.1.aemeasurable).aestronglyMeasurable

lemma ae_forall_integrable_exp_mul (h : HasSubgaussianMGF X c Îº Î½) :
    âˆ€áµ Ï‰' âˆ‚Î½, âˆ€ t, Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) (Îº Ï‰') := by
  have h_int (n : â„¤) : âˆ€áµ Ï‰' âˆ‚Î½, Integrable (fun Ï‰ â†¦ exp (n * X Ï‰)) (Îº Ï‰') :=
    h.ae_integrable_exp_mul _
  rw [â† ae_all_iff] at h_int
  filter_upwards [h_int] with Ï‰' h_int t
  exact integrable_exp_mul_of_le_of_le (h_int _) (h_int _) (Int.floor_le t) (Int.le_ceil t)

lemma memLp_exp_mul (h : HasSubgaussianMGF X c Îº Î½) (t : â„) (p : â„â‰¥0) :
    MemLp (fun Ï‰ â†¦ exp (t * X Ï‰)) p (Îº âˆ˜â‚˜ Î½) := by
  by_cases hp0 : p = 0
  Â· simpa [hp0] using (h.integrable_exp_mul t).1
  constructor
  Â· exact (h.integrable_exp_mul t).1
  Â· rw [eLpNorm_lt_top_iff_lintegral_rpow_enorm_lt_top (mod_cast hp0) (by simp)]
    simp only [ENNReal.coe_toReal]
    have h' := (h.integrable_exp_mul (p * t)).2
    rw [hasFiniteIntegral_def] at h'
    convert h' using 3 with Ï‰
    rw [enorm_eq_ofReal (by positivity), enorm_eq_ofReal (by positivity),
      ENNReal.ofReal_rpow_of_nonneg (by positivity), â† exp_mul, mul_comm, â† mul_assoc]
    positivity

lemma cgf_le (h : HasSubgaussianMGF X c Îº Î½) :
    âˆ€áµ Ï‰' âˆ‚Î½, âˆ€ t, cgf X (Îº Ï‰') t â‰¤ c * t ^ 2 / 2 := by
  filter_upwards [h.mgf_le, h.ae_forall_integrable_exp_mul] with Ï‰' h h_int t
  calc cgf X (Îº Ï‰') t
  _ = log (mgf X (Îº Ï‰') t) := rfl
  _ â‰¤ log (exp (c * t ^ 2 / 2)) := by
    by_cases h0 : Îº Ï‰' = 0
    Â· simpa [h0] using by positivity
    gcongr
    Â· exact mgf_pos' h0 (h_int t)
    Â· exact h t
  _ â‰¤ c * t ^ 2 / 2 := by rw [log_exp]

end BasicProperties

protected lemma of_rat (h_int : âˆ€ t : â„, Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) (Îº âˆ˜â‚˜ Î½))
    (h_mgf : âˆ€ q : â„š, âˆ€áµ Ï‰' âˆ‚Î½, mgf X (Îº Ï‰') q â‰¤ exp (c * q ^ 2 / 2)) :
    Kernel.HasSubgaussianMGF X c Îº Î½ where
  integrable_exp_mul := h_int
  mgf_le := by
    rw [â† ae_all_iff] at h_mgf
    have h_int : âˆ€áµ Ï‰' âˆ‚Î½, âˆ€ t, Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) (Îº Ï‰') := by
      have h_int' (n : â„¤) := Measure.ae_integrable_of_integrable_comp (h_int n)
      rw [â† ae_all_iff] at h_int'
      filter_upwards [h_int'] with Ï‰' h_int t
      exact integrable_exp_mul_of_le_of_le (h_int _) (h_int _) (Int.floor_le t) (Int.le_ceil t)
    filter_upwards [h_mgf, h_int] with Ï‰' h_mgf h_int t
    refine Rat.denseRange_cast.induction_on t ?_ h_mgf
    exact isClosed_le (continuous_mgf h_int) (by fun_prop)

@[simp]
lemma fun_zero [IsFiniteMeasure Î½] [IsZeroOrMarkovKernel Îº] :
    HasSubgaussianMGF (fun _ â†¦ 0) 0 Îº Î½ where
  integrable_exp_mul := by simp
  mgf_le := by simpa using ae_of_all _ fun _ â†¦ toReal_prob_le_one

@[simp]
lemma zero [IsFiniteMeasure Î½] [IsZeroOrMarkovKernel Îº] : HasSubgaussianMGF 0 0 Îº Î½ := fun_zero

lemma congr {Y : Î© â†’ â„} (h : HasSubgaussianMGF X c Îº Î½) (h' : X =áµ[Îº âˆ˜â‚˜ Î½] Y) :
    HasSubgaussianMGF Y c Îº Î½ where
  integrable_exp_mul t := by
    refine (integrable_congr ?_).mpr (h.integrable_exp_mul t)
    filter_upwards [h'] with Ï‰ hÏ‰ using by rw [hÏ‰]
  mgf_le := by
    have h'' := Measure.ae_ae_of_ae_comp h'
    filter_upwards [h.mgf_le, h''] with Ï‰' h_mgf h' t
    rw [mgf_congr (Filter.EventuallyEq.symm h')]
    exact h_mgf t

lemma _root_.ProbabilityTheory.Kernel.HasSubgaussianMGF_congr {Y : Î© â†’ â„} (h : X =áµ[Îº âˆ˜â‚˜ Î½] Y) :
    HasSubgaussianMGF X c Îº Î½ â†” HasSubgaussianMGF Y c Îº Î½ :=
  âŸ¨fun hX â†¦ congr hX h, fun hY â†¦ congr hY (ae_eq_symm h)âŸ©

section ChernoffBound

lemma measure_ge_le_exp_add [IsFiniteKernel Îº] (h : HasSubgaussianMGF X c Îº Î½) (Îµ : â„) :
    âˆ€áµ Ï‰' âˆ‚Î½, âˆ€ t, 0 â‰¤ t â†’ (Îº Ï‰' {Ï‰ | Îµ â‰¤ X Ï‰}).toReal â‰¤ exp (- t * Îµ + c * t ^ 2 / 2) := by
  filter_upwards [h.mgf_le, h.ae_forall_integrable_exp_mul] with Ï‰' h1 h2 t ht
  calc (Îº Ï‰' {Ï‰ | Îµ â‰¤ X Ï‰}).toReal
  _ â‰¤ exp (-t * Îµ) * mgf X (Îº Ï‰') t := measure_ge_le_exp_mul_mgf Îµ ht (h2 t)
  _ â‰¤ exp (-t * Îµ + c * t ^ 2 / 2) := by
    rw [exp_add]
    gcongr
    exact h1 t

/-- Chernoff bound on the right tail of a sub-Gaussian random variable. -/
lemma measure_ge_le [IsFiniteKernel Îº] (h : HasSubgaussianMGF X c Îº Î½) {Îµ : â„}
    (hc : 0 < c) (hÎµ : 0 â‰¤ Îµ) :
    âˆ€áµ Ï‰' âˆ‚Î½, (Îº Ï‰' {Ï‰ | Îµ â‰¤ X Ï‰}).toReal â‰¤ exp (- Îµ ^ 2 / (2 * c)) := by
  filter_upwards [measure_ge_le_exp_add h Îµ] with Ï‰' h
  calc (Îº Ï‰' {Ï‰ | Îµ â‰¤ X Ï‰}).toReal
  -- choose the minimizer of the r.h.s. of `h` for `t â‰¥ 0`. That is, `t = Îµ / c`.
  _ â‰¤ exp (- (Îµ / c) * Îµ + c * (Îµ / c) ^ 2 / 2) := h (Îµ / c) (by positivity)
  _ = exp (- Îµ ^ 2 / (2 * c)) := by congr; field_simp; ring

/-- Chernoff bound on the right tail of a sub-Gaussian random variable. -/
lemma prob_ge_le [IsZeroOrMarkovKernel Îº] (h : HasSubgaussianMGF X c Îº Î½)
    {Îµ : â„} (hÎµ : 0 â‰¤ Îµ) :
    âˆ€áµ Ï‰' âˆ‚Î½, (Îº Ï‰' {Ï‰ | Îµ â‰¤ X Ï‰}).toReal â‰¤ exp (- Îµ ^ 2 / (2 * c)) := by
  by_cases hc0 : c = 0
  Â· refine ae_of_all _ fun Ï‰' â†¦ ?_
    simpa [hc0] using toReal_prob_le_one
  Â· exact h.measure_ge_le (lt_of_le_of_ne zero_le' (Ne.symm hc0)) hÎµ

end ChernoffBound

end Kernel.HasSubgaussianMGF

end Kernel

section Conditional

/-! ### Conditionally sub-Gaussian moment generating function -/

variable {Î© : Type*} {m mÎ© : MeasurableSpace Î©} {hm : m â‰¤ mÎ©} [StandardBorelSpace Î©]
  {Î¼ : Measure Î©} [IsFiniteMeasure Î¼] {X : Î© â†’ â„} {c : â„â‰¥0}

variable (m) (hm) in
/-- A random variable `X` has a conditionally sub-Gaussian moment generating function
with parameter `c` with respect to a sigma-algebra `m` and a measure `Î¼` if for all `t : â„`,
`exp (t * X)` is `Î¼`-integrable and the moment generating function of `X` contioned on `m` is
almost surely bounded by `exp (c * t ^ 2 / 2)` for all `t : â„`.

The actual definition uses `Kernel.HasSubgaussianMGF`: `HasCondSubgaussianMGF` is defined as
sub-Gaussian with respect to the conditional expectation kernel for `m` and the restriction of `Î¼`
to the sigma-algebra `m`. -/
def HasCondSubgaussianMGF (X : Î© â†’ â„) (c : â„â‰¥0)
    (Î¼ : Measure Î© := by volume_tac) [IsFiniteMeasure Î¼] : Prop :=
  Kernel.HasSubgaussianMGF X c (condExpKernel Î¼ m) (Î¼.trim hm)

namespace HasCondSubgaussianMGF

lemma mgf_le (h : HasCondSubgaussianMGF m hm X c Î¼) :
    âˆ€áµ Ï‰' âˆ‚(Î¼.trim hm), âˆ€ t, mgf X (condExpKernel Î¼ m Ï‰') t â‰¤ exp (c * t ^ 2 / 2) :=
  Kernel.HasSubgaussianMGF.mgf_le h

lemma cgf_le (h : HasCondSubgaussianMGF m hm X c Î¼) :
    âˆ€áµ Ï‰' âˆ‚(Î¼.trim hm), âˆ€ t, cgf X (condExpKernel Î¼ m Ï‰') t â‰¤ c * t ^ 2 / 2 :=
  Kernel.HasSubgaussianMGF.cgf_le h

lemma ae_trim_condExp_le (h : HasCondSubgaussianMGF m hm X c Î¼) (t : â„) :
    âˆ€áµ Ï‰' âˆ‚(Î¼.trim hm), (Î¼[fun Ï‰ â†¦ exp (t * X Ï‰) | m]) Ï‰' â‰¤ exp (c * t ^ 2 / 2) := by
  have h_eq := condExp_ae_eq_trim_integral_condExpKernel hm (h.integrable_exp_mul t)
  simp_rw [condExpKernel_comp_trim] at h_eq
  filter_upwards [h.mgf_le, h_eq] with Ï‰' h_mgf h_eq
  rw [h_eq]
  exact h_mgf t

lemma ae_condExp_le (h : HasCondSubgaussianMGF m hm X c Î¼) (t : â„) :
    âˆ€áµ Ï‰' âˆ‚Î¼, (Î¼[fun Ï‰ â†¦ exp (t * X Ï‰) | m]) Ï‰' â‰¤ exp (c * t ^ 2 / 2) :=
  ae_of_ae_trim hm (h.ae_trim_condExp_le t)

@[simp]
lemma fun_zero : HasCondSubgaussianMGF m hm (fun _ â†¦ 0) 0 Î¼ := Kernel.HasSubgaussianMGF.fun_zero

@[simp]
lemma zero : HasCondSubgaussianMGF m hm 0 0 Î¼ := Kernel.HasSubgaussianMGF.zero

lemma memLp_exp_mul (h : HasCondSubgaussianMGF m hm X c Î¼) (t : â„) (p : â„â‰¥0) :
    MemLp (fun Ï‰ â†¦ exp (t * X Ï‰)) p Î¼ :=
  condExpKernel_comp_trim (Î¼ := Î¼) hm â–¸ Kernel.HasSubgaussianMGF.memLp_exp_mul h t p

lemma integrable_exp_mul (h : HasCondSubgaussianMGF m hm X c Î¼) (t : â„) :
    Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼ :=
  condExpKernel_comp_trim (Î¼ := Î¼) hm â–¸ Kernel.HasSubgaussianMGF.integrable_exp_mul h t

end HasCondSubgaussianMGF

end Conditional

/-! ### Sub-Gaussian moment generating function -/

variable {Î© : Type*} {m mÎ© : MeasurableSpace Î©} {Î¼ : Measure Î©} {X : Î© â†’ â„} {c : â„â‰¥0}

/-- A random variable `X` has a sub-Gaussian moment generating function with parameter `c`
with respect to a measure `Î¼` if for all `t : â„`, `exp (t * X)` is `Î¼`-integrable and
the moment generating function of `X` is bounded by `exp (c * t ^ 2 / 2)` for all `t : â„`.

This is equivalent to `Kernel.HasSubgaussianMGF X c (Kernel.const Unit Î¼) (Measure.dirac ())`,
as proved in `HasSubgaussianMGF_iff_kernel`.
Properties about sub-Gaussian moment generating functions should be proved first for
`Kernel.HasSubgaussianMGF` when possible. -/
structure HasSubgaussianMGF (X : Î© â†’ â„) (c : â„â‰¥0) (Î¼ : Measure Î© := by volume_tac) : Prop where
  integrable_exp_mul : âˆ€ t : â„, Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼
  mgf_le : âˆ€ t : â„, mgf X Î¼ t â‰¤ exp (c * t ^ 2 / 2)

lemma HasSubgaussianMGF_iff_kernel :
    HasSubgaussianMGF X c Î¼
      â†” Kernel.HasSubgaussianMGF X c (Kernel.const Unit Î¼) (Measure.dirac ()) :=
  âŸ¨fun âŸ¨h1, h2âŸ© â†¦ âŸ¨by simpa, by simpaâŸ©, fun âŸ¨h1, h2âŸ© â†¦ âŸ¨by simpa using h1, by simpa using h2âŸ©âŸ©

namespace HasSubgaussianMGF

lemma aestronglyMeasurable (h : HasSubgaussianMGF X c Î¼) : AEStronglyMeasurable X Î¼ := by
  have h_int := h.integrable_exp_mul 1
  simpa using (aemeasurable_of_aemeasurable_exp h_int.1.aemeasurable).aestronglyMeasurable

lemma memLp_exp_mul (h : HasSubgaussianMGF X c Î¼) (t : â„) (p : â„â‰¥0) :
    MemLp (fun Ï‰ â†¦ exp (t * X Ï‰)) p Î¼ := by
  rw [HasSubgaussianMGF_iff_kernel] at h
  simpa using h.memLp_exp_mul t p

lemma cgf_le (h : HasSubgaussianMGF X c Î¼) (t : â„) : cgf X Î¼ t â‰¤ c * t ^ 2 / 2 := by
  rw [HasSubgaussianMGF_iff_kernel] at h
  simpa using (all_ae_of h.cgf_le t)

@[simp]
lemma fun_zero [IsZeroOrProbabilityMeasure Î¼] : HasSubgaussianMGF (fun _ â†¦ 0) 0 Î¼ := by
  simp [HasSubgaussianMGF_iff_kernel]

@[simp]
lemma zero [IsZeroOrProbabilityMeasure Î¼] : HasSubgaussianMGF 0 0 Î¼ := fun_zero

section ChernoffBound

/-- Chernoff bound on the right tail of a sub-Gaussian random variable. -/
lemma measure_ge_le [IsFiniteMeasure Î¼] (h : HasSubgaussianMGF X c Î¼) {Îµ : â„}
    (hc : 0 < c) (hÎµ : 0 â‰¤ Îµ) :
    (Î¼ {Ï‰ | Îµ â‰¤ X Ï‰}).toReal â‰¤ exp (- Îµ ^ 2 / (2 * c)) := by
  rw [HasSubgaussianMGF_iff_kernel] at h
  simpa using h.measure_ge_le hc hÎµ

/-- Chernoff bound on the right tail of a sub-Gaussian random variable. -/
lemma prob_ge_le [IsZeroOrProbabilityMeasure Î¼] (h : HasSubgaussianMGF X c Î¼) {Îµ : â„} (hÎµ : 0 â‰¤ Îµ) :
    (Î¼ {Ï‰ | Îµ â‰¤ X Ï‰}).toReal â‰¤ exp (- Îµ ^ 2 / (2 * c)) := by
  rw [HasSubgaussianMGF_iff_kernel] at h
  simpa using h.prob_ge_le hÎµ

end ChernoffBound

end HasSubgaussianMGF

end ProbabilityTheory
