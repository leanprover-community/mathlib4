/-
Copyright (c) 2025 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import Mathlib.Probability.Independence.Basic
import Mathlib.Probability.Kernel.Composition.MeasureComp
import Mathlib.Probability.Kernel.Condexp
import Mathlib.Probability.Martingale.Basic
import Mathlib.Probability.Moments.MGFAnalytic

/-!
# Sub-Gaussian random variables

This presentation of sub-Gaussian random variables is inspired by section 2.5 of
[vershynin2018high]. Let `X` be a random variable. Consider the following five properties, in which
`K·µ¢` are positive reals,
* (i) for all `t ‚â• 0`, `‚Ñô(|X| ‚â• t) ‚â§ 2 * exp(-t^2 / K‚ÇÅ^2)`,
* (ii) for all `p : ‚Ñï` with `1 ‚â§ p`, `ùîº[|X|^p]^(1/p) ‚â§ K‚ÇÇ sqrt(p)`,
* (iii) for all `|t| ‚â§ 1/K‚ÇÉ`, `ùîº[exp (t^2 * X^2)] ‚â§ exp (K‚ÇÉ^2 * t^2)`,
* (iv) `ùîº[exp(X^2 / K‚ÇÑ)] ‚â§ 2`,
* (v) for all `t : ‚Ñù`, `ùîº[exp (t * X)] ‚â§ exp (K‚ÇÖ * t^2 / 2)`.

Properties (i) to (iv) are equivalent, in the sense that there exists a constant `C` such that
if `X` satisfies one of those properties with constant `K`, then it satisfies any other one with
constant at most `CK`.

If `ùîº[X] = 0` then properties (i)-(iv) are equivalent to (v) in that same sense.

The name sub-Gaussian is used by various authors to refer to any one of (i)-(v). We will say that a
random variable has sub-Gaussian moment generating function (mgf) with constant `K‚ÇÖ` to mean that
property (v) holds with that constant. The function `exp (K‚ÇÖ * t^2 / 2)` which appears in
property (v) is the mgf of a Gaussian with variance `K‚ÇÖ`.

TODO: implement (i)-(iv) and prove relations between those properties.

TODO TODO: adapt this text to the new implementation. Talk about kernels, conditional sub-G, sub-G.

## Main definitions

*

## Main statements

*

## References

* [R. Vershynin, *High-dimensional probability: An introduction with applications in data
science*][vershynin2018high]

-/

open MeasureTheory Real

open scoped ENNReal NNReal Topology

namespace ProbabilityTheory

variable {Œ© Œ©' : Type*} (m : MeasurableSpace Œ©) {m1 m2 mŒ© : MeasurableSpace Œ©} (hm : m ‚â§ mŒ©)
  {mŒ©' : MeasurableSpace Œ©'}
  {Œº : Measure Œ©} {ŒΩ : Measure Œ©'} {Œ∫ : Kernel Œ©' Œ©} {X : Œ© ‚Üí ‚Ñù} {c : ‚Ñù‚â•0} {Œµ : ‚Ñù}

-- todo: fix measurable space arguments in Measure.bind and in Measure.snd_map_prod_mk
lemma condExpKernel_comp_trim [StandardBorelSpace Œ©] [IsFiniteMeasure Œº] :
    @Measure.bind _ _ m mŒ© (Œº.trim hm) (condExpKernel Œº m) = Œº := by
  rw [‚Üê Measure.snd_compProd, compProd_trim_condExpKernel, @Measure.snd_map_prod_mk, Measure.map_id]
  exact measurable_id'' hm

-- todo: delete?
theorem condExp_ae_eq_trim_integral_condExpKernel {F : Type*} [NormedAddCommGroup F] {f : Œ© ‚Üí F}
    [NormedSpace ‚Ñù F] [CompleteSpace F]
    [StandardBorelSpace Œ©] [IsFiniteMeasure Œº]
    (hm : m ‚â§ mŒ©) (hf : StronglyMeasurable f) (hf_int : Integrable f Œº) :
    Œº[f|m] =·µê[Œº.trim hm] fun œâ ‚Ü¶ ‚à´ y, f y ‚àÇcondExpKernel Œº m œâ :=
  StronglyMeasurable.ae_eq_trim_of_stronglyMeasurable hm stronglyMeasurable_condExp
      hf.integral_condExpKernel (condExp_ae_eq_integral_condExpKernel hm hf_int)

@[simp]
lemma prodMkLeft_comp_compProd {Œ©'' : Type*} {mŒ©'' : MeasurableSpace Œ©''}
    {Œ∑ : Kernel Œ© Œ©''} [SFinite ŒΩ] [IsSFiniteKernel Œ∫] :
    (Œ∑.prodMkLeft Œ©') ‚àò‚Çò ŒΩ ‚äó‚Çò Œ∫ = Œ∑ ‚àò‚Çò Œ∫ ‚àò‚Çò ŒΩ := by
  conv_rhs => rw [‚Üê Measure.snd_compProd (Œº := ŒΩ)]
  rw [Kernel.prodMkLeft, Measure.snd, ‚Üê Measure.deterministic_comp_eq_map measurable_snd,
    Measure.comp_assoc, Kernel.comp_deterministic_eq_comap]

section Kernel

/-! ### Sub-Gaussian with respect to a kernel and a measure -/

/-- A random variable is sub-Gaussian with parameter `c` with respect to a kernel `Œ∫` and
a measure `ŒΩ` if `ŒΩ`-almost surely, for all `t : ‚Ñù`, the moment generating function of `X`
with respect to `Œ∫` is bounded by `exp (c * t ^ 2 / 2)`. -/
structure Kernel.IsSubGaussianWith (X : Œ© ‚Üí ‚Ñù) (c : ‚Ñù‚â•0)
    (Œ∫ : Kernel Œ©' Œ©) (ŒΩ : Measure Œ©' := by volume_tac) : Prop where
  integrable_exp_mul : ‚àÄ t : ‚Ñù, Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) (Œ∫ ‚àò‚Çò ŒΩ)
  mgf_le : ‚àÄ·µê œâ' ‚àÇŒΩ, ‚àÄ t : ‚Ñù, mgf X (Œ∫ œâ') t ‚â§ exp (c * t ^ 2 / 2)

def Kernel.IsSubGaussian (X : Œ© ‚Üí ‚Ñù) (Œ∫ : Kernel Œ©' Œ©) (ŒΩ : Measure Œ©' := by volume_tac) : Prop :=
  ‚àÉ c : ‚Ñù‚â•0, Kernel.IsSubGaussianWith X c Œ∫ ŒΩ

namespace Kernel.IsSubGaussianWith

lemma aestronglyMeasurable (h : IsSubGaussianWith X c Œ∫ ŒΩ) : AEStronglyMeasurable X (Œ∫ ‚àò‚Çò ŒΩ) := by
  have h_int := h.integrable_exp_mul 1
  simp only [one_mul] at h_int
  exact (aemeasurable_of_aemeasurable_exp h_int.1.aemeasurable).aestronglyMeasurable

lemma ae_integrable_exp_mul [SFinite ŒΩ] [IsSFiniteKernel Œ∫]
    (h : IsSubGaussianWith X c Œ∫ ŒΩ) (t : ‚Ñù) :
    ‚àÄ·µê œâ' ‚àÇŒΩ, Integrable (fun y ‚Ü¶ exp (t * X y)) (Œ∫ œâ') :=
  Measure.ae_integrable_of_integrable_comp (h.integrable_exp_mul t)

lemma ae_aestronglyMeasurable [SFinite ŒΩ] [IsSFiniteKernel Œ∫] (h : IsSubGaussianWith X c Œ∫ ŒΩ) :
    ‚àÄ·µê œâ' ‚àÇŒΩ, AEStronglyMeasurable X (Œ∫ œâ') := by
  have h_int := h.ae_integrable_exp_mul 1
  simp only [one_mul] at h_int
  filter_upwards [h_int] with œâ h_int
  exact (aemeasurable_of_aemeasurable_exp h_int.1.aemeasurable).aestronglyMeasurable

lemma ae_forall_integrable_exp_mul [SFinite ŒΩ] [IsSFiniteKernel Œ∫] (h : IsSubGaussianWith X c Œ∫ ŒΩ) :
    ‚àÄ·µê œâ' ‚àÇŒΩ, ‚àÄ t : ‚Ñù, Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) (Œ∫ œâ') := by
  have h_int : ‚àÄ n : ‚Ñ§, ‚àÄ·µê œâ' ‚àÇŒΩ, Integrable (fun œâ ‚Ü¶ exp (n * X œâ)) (Œ∫ œâ') :=
    fun _ ‚Ü¶ h.ae_integrable_exp_mul _
  rw [‚Üê ae_all_iff] at h_int
  filter_upwards [h_int] with œâ' h_int t
  exact integrable_exp_mul_of_le_of_le (h_int _) (h_int _) (Int.floor_le t) (Int.le_ceil t)

lemma integrableExpSet_eq_univ [SFinite ŒΩ] [IsSFiniteKernel Œ∫] (h : IsSubGaussianWith X c Œ∫ ŒΩ) :
    ‚àÄ·µê œâ' ‚àÇŒΩ, integrableExpSet X (Œ∫ œâ') = Set.univ := by
  filter_upwards [h.ae_forall_integrable_exp_mul] with œâ' h_int
  ext t
  simp [h_int t, integrableExpSet]

lemma integrable_exp_mul_of_int
    (h_int : ‚àÄ n : ‚Ñ§, ‚àÄ·µê œâ' ‚àÇŒΩ, Integrable (fun œâ ‚Ü¶ exp (n * X œâ)) (Œ∫ œâ')) :
    ‚àÄ·µê œâ' ‚àÇŒΩ, ‚àÄ t, Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) (Œ∫ œâ') := by
  rw [‚Üê ae_all_iff] at h_int
  filter_upwards [h_int] with œâ' h_int t
  exact integrable_exp_mul_of_le_of_le (h_int _) (h_int _) (Int.floor_le t) (Int.le_ceil t)

protected lemma of_rat [SFinite ŒΩ] [IsSFiniteKernel Œ∫]
    (h_int : ‚àÄ t : ‚Ñù, Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) (Œ∫ ‚àò‚Çò ŒΩ))
    (h_mgf : ‚àÄ q : ‚Ñö, ‚àÄ·µê œâ' ‚àÇŒΩ, mgf X (Œ∫ œâ') q ‚â§ exp (c * q ^ 2 / 2)) :
    Kernel.IsSubGaussianWith X c Œ∫ ŒΩ where
  integrable_exp_mul := h_int
  mgf_le := by
    rw [‚Üê ae_all_iff] at h_mgf
    have h_int : ‚àÄ·µê œâ' ‚àÇŒΩ, ‚àÄ t, Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) (Œ∫ œâ') := by
      refine integrable_exp_mul_of_int (fun n ‚Ü¶ ?_)
      exact Measure.ae_integrable_of_integrable_comp (h_int n)
    filter_upwards [h_mgf, h_int]
      with œâ' h_mgf h_int t
    refine Rat.denseRange_cast.induction_on t ?_ h_mgf
    refine isClosed_le ?_ (by fun_prop)
    exact continuous_mgf fun u ‚Ü¶ h_int _

protected lemma mem‚Ñíp (h : IsSubGaussianWith X c Œ∫ ŒΩ) (t : ‚Ñù) (p : ‚Ñù‚â•0) :
    Mem‚Ñíp (fun œâ ‚Ü¶ exp (t * X œâ)) p (Œ∫ ‚àò‚Çò ŒΩ) := by
  by_cases hp0 : p = 0
  ¬∑ simp only [hp0, ENNReal.coe_zero, mem‚Ñíp_zero_iff_aestronglyMeasurable]
    exact (h.integrable_exp_mul t).1
  constructor
  ¬∑ exact (h.integrable_exp_mul t).1
  ¬∑ rw [eLpNorm_lt_top_iff_lintegral_rpow_enorm_lt_top (mod_cast hp0) (by simp)]
    simp only [ENNReal.coe_toReal]
    have h' := (h.integrable_exp_mul (p * t)).2
    rw [hasFiniteIntegral_def] at h'
    convert h' using 3 with p
    rw [enorm_eq_ofReal (by positivity), enorm_eq_ofReal (by positivity),
      ENNReal.ofReal_rpow_of_nonneg (by positivity), ‚Üê exp_mul, mul_comm, ‚Üê mul_assoc]
    simp

lemma cgf_le [SFinite ŒΩ] [IsSFiniteKernel Œ∫] (h : IsSubGaussianWith X c Œ∫ ŒΩ) (t : ‚Ñù) :
    ‚àÄ·µê œâ' ‚àÇŒΩ, cgf X (Œ∫ œâ') t ‚â§ c * t ^ 2 / 2 := by
  filter_upwards [h.mgf_le, h.ae_forall_integrable_exp_mul] with œâ' h h_int
  calc cgf X (Œ∫ œâ') t
  _ = log (mgf X (Œ∫ œâ') t) := rfl
  _ ‚â§ log (exp (c * t ^ 2 / 2)) := by
    by_cases h0 : Œ∫ œâ' = 0
    ¬∑ simp only [h0, mgf_zero_measure, Pi.zero_apply, log_zero, log_exp]
      positivity
    gcongr
    ¬∑ exact mgf_pos' h0 (h_int t)
    ¬∑ exact h t
  _ ‚â§ c * t ^ 2 / 2 := by rw [log_exp]

@[simp]
lemma zero [IsFiniteMeasure ŒΩ] [IsZeroOrMarkovKernel Œ∫] : IsSubGaussianWith (fun _ ‚Ü¶ 0) 0 Œ∫ ŒΩ := by
  refine .of_rat ?_ ?_
  ¬∑ simp
  ¬∑ refine fun q ‚Ü¶ ?_
    simp only [mgf_const', mul_zero, exp_zero, mul_one, NNReal.coe_zero, zero_mul, zero_div]
    exact ae_of_all _ fun _ ‚Ü¶ toReal_prob_le_one

@[simp]
lemma zero' [IsFiniteMeasure ŒΩ] [IsZeroOrMarkovKernel Œ∫] : IsSubGaussianWith 0 0 Œ∫ ŒΩ := zero

lemma congr [SFinite ŒΩ] [IsSFiniteKernel Œ∫] {Y : Œ© ‚Üí ‚Ñù} (h : IsSubGaussianWith X c Œ∫ ŒΩ)
    (h' : X =·µê[Œ∫ ‚àò‚Çò ŒΩ] Y) :
    IsSubGaussianWith Y c Œ∫ ŒΩ where
  integrable_exp_mul t := by
    refine (integrable_congr ?_).mpr (h.integrable_exp_mul t)
    filter_upwards [h'] with œâ' hœâ'
    rw [hœâ']
  mgf_le := by
    have h'' := Measure.ae_ae_of_ae_comp h'
    filter_upwards [h.mgf_le, h''] with œâ' h_mgf h' t
    rw [mgf_congr (Filter.EventuallyEq.symm h')]
    exact h_mgf t

lemma _root_.ProbabilityTheory.Kernel.isSubGaussianWith_congr [SFinite ŒΩ] [IsSFiniteKernel Œ∫]
    {Y : Œ© ‚Üí ‚Ñù} (h : X =·µê[Œ∫ ‚àò‚Çò ŒΩ] Y) :
    IsSubGaussianWith X c Œ∫ ŒΩ ‚Üî IsSubGaussianWith Y c Œ∫ ŒΩ :=
  ‚ü®fun hX ‚Ü¶ congr hX h, fun hY ‚Ü¶ congr hY <| by filter_upwards [h] with œâ' hœâ' using hœâ'.symm‚ü©

lemma id_map (hX : Measurable X) :
    IsSubGaussianWith id c (Œ∫.map X) ŒΩ ‚Üî IsSubGaussianWith X c Œ∫ ŒΩ := by
  have h_map : (Œ∫.map X) ‚àò‚Çò ŒΩ = (Œ∫ ‚àò‚Çò ŒΩ).map X := by
    rw [‚Üê deterministic_comp_eq_map hX, ‚Üê Measure.comp_assoc, Measure.deterministic_comp_eq_map]
  refine ‚ü®fun ‚ü®h1, h2‚ü© ‚Ü¶ ‚ü®fun t ‚Ü¶ ?_, ?_‚ü©, fun ‚ü®h1, h2‚ü© ‚Ü¶ ‚ü®fun t ‚Ü¶ ?_, ?_‚ü©‚ü©
  ¬∑ specialize h1 t
    rw [h_map] at h1
    rwa [integrable_map_measure] at h1
    ¬∑ exact Measurable.aestronglyMeasurable <| by fun_prop
    ¬∑ fun_prop
  ¬∑ simpa [Kernel.map_apply _ hX, mgf_id_map hX.aemeasurable] using h2
  ¬∑ specialize h1 t
    rwa [h_map, integrable_map_measure]
    ¬∑ exact Measurable.aestronglyMeasurable <| by fun_prop
    ¬∑ fun_prop
  ¬∑ simpa [Kernel.map_apply _ hX, mgf_id_map hX.aemeasurable] using h2

lemma measure_ge_le_exp_add [SFinite ŒΩ] [IsFiniteKernel Œ∫] (h : IsSubGaussianWith X c Œ∫ ŒΩ) (Œµ : ‚Ñù) :
    ‚àÄ·µê œâ' ‚àÇŒΩ, ‚àÄ t, 0 ‚â§ t ‚Üí (Œ∫ œâ' {œâ | Œµ ‚â§ X œâ}).toReal ‚â§ exp (- t * Œµ + c * t ^ 2 / 2) := by
  filter_upwards [h.mgf_le, h.ae_forall_integrable_exp_mul] with œâ' h1 h2 t ht
  calc (Œ∫ œâ' {œâ | Œµ ‚â§ X œâ}).toReal
  _ ‚â§ exp (-t * Œµ) * mgf X (Œ∫ œâ') t := measure_ge_le_exp_mul_mgf Œµ ht (h2 t)
  _ ‚â§ exp (-t * Œµ + c * t ^ 2 / 2) := by
    rw [exp_add]
    gcongr
    exact h1 t

/-- Chernoff bound on the right tail of a sub-Gaussian random variable. -/
lemma measure_ge_le [SFinite ŒΩ] [IsFiniteKernel Œ∫] (h : IsSubGaussianWith X c Œ∫ ŒΩ) {Œµ : ‚Ñù}
    (hc : 0 < c) (hŒµ : 0 ‚â§ Œµ) :
    ‚àÄ·µê œâ' ‚àÇŒΩ, (Œ∫ œâ' {œâ | Œµ ‚â§ X œâ}).toReal ‚â§ exp (- Œµ ^ 2 / (2 * c)) := by
  filter_upwards [measure_ge_le_exp_add h Œµ] with œâ' h
  calc (Œ∫ œâ' {œâ | Œµ ‚â§ X œâ}).toReal
  -- choose the minimizer of the r.h.s. of `h` for `t ‚â• 0`. That is, `t = Œµ / c`.
  _ ‚â§ exp (- (Œµ / c) * Œµ + c * (Œµ / c) ^ 2 / 2) := h (Œµ / c) (by positivity)
  _ = exp (- Œµ ^ 2 / (2 * c)) := by congr; field_simp; ring

lemma prob_ge_le [SFinite ŒΩ] [IsMarkovKernel Œ∫] (h : IsSubGaussianWith X c Œ∫ ŒΩ) (hŒµ : 0 ‚â§ Œµ) :
    ‚àÄ·µê œâ' ‚àÇŒΩ, (Œ∫ œâ' {œâ | Œµ ‚â§ X œâ}).toReal ‚â§ exp (- Œµ ^ 2 / (2 * c)) := by
  by_cases hc0 : c = 0
  ¬∑ refine ae_of_all _ fun œâ' ‚Ü¶ ?_
    simpa [hc0] using toReal_prob_le_one
  ¬∑ exact h.measure_ge_le (lt_of_le_of_ne zero_le' (Ne.symm hc0)) hŒµ

section Add

variable {Œ©'' : Type*} {mŒ©'' : MeasurableSpace Œ©''} {Y : Œ©'' ‚Üí ‚Ñù} {cY : ‚Ñù‚â•0}
  [SFinite ŒΩ] [IsSFiniteKernel Œ∫]

lemma prodMkLeft_compProd {Œ∑ : Kernel Œ© Œ©''} (h : IsSubGaussianWith Y cY Œ∑ (Œ∫ ‚àò‚Çò ŒΩ)) :
    IsSubGaussianWith Y cY (prodMkLeft Œ©' Œ∑) (ŒΩ ‚äó‚Çò Œ∫) := by
  constructor
  ¬∑ convert h.integrable_exp_mul
    simp
  ¬∑ have h2 := h.mgf_le
    simp only [prodMkLeft_apply] at h2
    rw [‚Üê Measure.snd_compProd, Measure.snd] at h2
    refine ae_of_ae_map ?_ h2
    fun_prop

lemma integrable_exp_add_compProd {Œ∑ : Kernel (Œ©' √ó Œ©) Œ©''} [IsMarkovKernel Œ∑]
    (hX : IsSubGaussianWith X c Œ∫ ŒΩ) (hY : IsSubGaussianWith Y cY Œ∑ (ŒΩ ‚äó‚Çò Œ∫)) (t : ‚Ñù) :
    Integrable (fun œâ ‚Ü¶ exp (t * (X œâ.1 + Y œâ.2))) (‚áë(Œ∫ ‚äó‚Çñ Œ∑) ‚àò‚Çò ŒΩ) := by
  simp_rw [mul_add, exp_add]
  refine Mem‚Ñíp.integrable_mul ?_ ?_
  ¬∑ have h := hX.mem‚Ñíp t 2
    simp only [ENNReal.coe_ofNat] at h
    have : Œ∫ ‚àò‚Çò ŒΩ = ((Œ∫ ‚äó‚Çñ Œ∑) ‚àò‚Çò ŒΩ).map Prod.fst := by
      rw [Measure.map_comp _ _ measurable_fst, ‚Üê fst_eq, fst_compProd]
    rwa [this, mem‚Ñíp_map_measure_iff h.1 measurable_fst.aemeasurable] at h
  ¬∑ have h := hY.mem‚Ñíp t 2
    simp only [ENNReal.coe_ofNat] at h
    rwa [Measure.comp_compProd_comm, Measure.snd,
      mem‚Ñíp_map_measure_iff h.1 measurable_snd.aemeasurable] at h

lemma add {Œ∑ : Kernel (Œ©' √ó Œ©) Œ©''} [IsMarkovKernel Œ∑]
    (hX : IsSubGaussianWith X c Œ∫ ŒΩ) (hY : IsSubGaussianWith Y cY Œ∑ (ŒΩ ‚äó‚Çò Œ∫)) :
    IsSubGaussianWith (fun p ‚Ü¶ X p.1 + Y p.2) (c + cY) (Œ∫ ‚äó‚Çñ Œ∑) ŒΩ := by
  refine .of_rat (integrable_exp_add_compProd hX hY) ?_
  intro q
  filter_upwards [hX.mgf_le, hX.ae_integrable_exp_mul q, Measure.ae_ae_of_ae_compProd hY.mgf_le,
    Measure.ae_integrable_of_integrable_comp <| integrable_exp_add_compProd hX hY q]
    with œâ' hX_mgf hX_int hY_mgf h_int_mul
  calc mgf (fun p ‚Ü¶ X p.1 + Y p.2) ((Œ∫ ‚äó‚Çñ Œ∑) œâ') q
  _ = ‚à´ x, exp (q * X x) * ‚à´ y, exp (q * Y y) ‚àÇ(Œ∑ (œâ', x)) ‚àÇ(Œ∫ œâ') := by
    simp_rw [mgf, mul_add, exp_add] at h_int_mul ‚ä¢
    simp_rw [integral_compProd h_int_mul, integral_mul_left]
  _ ‚â§ ‚à´ x, exp (q * X x) * exp (cY * q ^ 2 / 2) ‚àÇ(Œ∫ œâ') := by
    refine integral_mono_of_nonneg ?_ (hX_int.mul_const _) ?_
    ¬∑ exact ae_of_all _ fun  œâ ‚Ü¶ mul_nonneg (by positivity)
        (integral_nonneg (fun _ ‚Ü¶ by positivity))
    ¬∑ filter_upwards [all_ae_of hY_mgf q] with œâ hY_mgf
      gcongr
      exact hY_mgf
  _ ‚â§ exp (‚Üë(c + cY) * q ^ 2 / 2) := by
    rw [integral_mul_right, NNReal.coe_add, add_mul, add_div, exp_add]
    gcongr
    exact hX_mgf q

lemma add' {Œ∑ : Kernel Œ© Œ©''} [IsMarkovKernel Œ∑]
    (hX : IsSubGaussianWith X c Œ∫ ŒΩ) (hY : IsSubGaussianWith Y cY Œ∑ (Œ∫ ‚àò‚Çò ŒΩ)) :
    IsSubGaussianWith (fun p ‚Ü¶ X p.1 + Y p.2) (c + cY) (Œ∫ ‚äó‚Çñ prodMkLeft Œ©' Œ∑) ŒΩ :=
  hX.add (prodMkLeft_compProd hY)

end Add

section Indep

lemma add_of_indepFun {Y : Œ© ‚Üí ‚Ñù} {cX cY : ‚Ñù‚â•0} [SFinite ŒΩ] [IsSFiniteKernel Œ∫]
    (hX : IsSubGaussianWith X cX Œ∫ ŒΩ) (hY : IsSubGaussianWith Y cY Œ∫ ŒΩ)
    (hindep : IndepFun X Y Œ∫ ŒΩ) :
    IsSubGaussianWith (X + Y) (cX + cY) Œ∫ ŒΩ := by
  have h_expX t : ‚àÉ X', StronglyMeasurable X'
      ‚àß ‚àÄ·µê œâ' ‚àÇŒΩ, (fun œâ ‚Ü¶ exp (t * X œâ)) =·∂†[ae (Œ∫ œâ')] X' := by
    obtain ‚ü®X', hX', hXX'‚ü© := hX.aestronglyMeasurable
    refine ‚ü®fun œâ ‚Ü¶ exp (t * X' œâ), continuous_exp.comp_stronglyMeasurable (hX'.const_mul _), ?_‚ü©
    filter_upwards [Measure.ae_ae_of_ae_comp hXX'] with œâ' hœâ'
    filter_upwards [hœâ'] with œâ hœâ
    rw [hœâ]
  have h_expY t : ‚àÉ Y', StronglyMeasurable Y'
      ‚àß ‚àÄ·µê œâ' ‚àÇŒΩ, (fun œâ ‚Ü¶ exp (t * Y œâ)) =·∂†[ae (Œ∫ œâ')] Y' := by
    obtain ‚ü®Y', hY', hYY'‚ü© := hY.aestronglyMeasurable
    refine ‚ü®fun œâ ‚Ü¶ exp (t * Y' œâ), continuous_exp.comp_stronglyMeasurable (hY'.const_mul _), ?_‚ü©
    filter_upwards [Measure.ae_ae_of_ae_comp hYY'] with œâ' hœâ'
    filter_upwards [hœâ'] with œâ hœâ
    rw [hœâ]
  refine .of_rat ?_ ?_
  ¬∑ intro t
    simp_rw [Pi.add_apply, mul_add, exp_add]
    exact Mem‚Ñíp.integrable_mul (hX.mem‚Ñíp t 2) (hY.mem‚Ñíp t 2)
  ¬∑ intro q
    have h := hindep.mgf_add (h_expX q) (h_expY q)
    filter_upwards [h, hX.mgf_le, hY.mgf_le] with œâ' h hX hY
    calc mgf (X + Y) (Œ∫ œâ') q
    _ = mgf X (Œ∫ œâ') q * mgf Y (Œ∫ œâ') q := by rw [h]
    _ ‚â§ exp (cX * q ^ 2 / 2) * exp (cY * q ^ 2 / 2) := by
      gcongr
      ¬∑ exact mgf_nonneg
      ¬∑ exact hX q
      ¬∑ exact hY q
    _ = exp ((cX + cY) * q ^ 2 / 2) := by
      rw [‚Üê exp_add]
      congr
      ring

lemma sum_of_iIndepFun {Œπ : Type*} [IsFiniteMeasure ŒΩ] [IsZeroOrMarkovKernel Œ∫]
    {X : Œπ ‚Üí Œ© ‚Üí ‚Ñù} (h_indep : iIndepFun (fun _ ‚Ü¶ inferInstance) X Œ∫ ŒΩ) {c : Œπ ‚Üí ‚Ñù‚â•0}
    (h_meas : ‚àÄ i, Measurable (X i))
    {s : Finset Œπ} (h_subG : ‚àÄ i ‚àà s, IsSubGaussianWith (X i) (c i) Œ∫ ŒΩ) :
    IsSubGaussianWith (‚àë i ‚àà s, X i) (‚àë i ‚àà s, c i) Œ∫ ŒΩ := by
  classical
  induction s using Finset.induction_on with
  | empty => simp
  | @insert i s his h =>
    rw [Finset.sum_insert his, Finset.sum_insert his]
    have h_indep' := (h_indep.indepFun_finset_sum_of_not_mem h_meas his).symm
    refine add_of_indepFun (h_subG _ (Finset.mem_insert_self _ _)) (h ?_) h_indep'
    exact fun i hi ‚Ü¶ h_subG _ (Finset.mem_insert_of_mem hi)

end Indep

end Kernel.IsSubGaussianWith

end Kernel

section Conditional

variable [StandardBorelSpace Œ©] [IsFiniteMeasure Œº]

def IsCondSubGaussianWith (X : Œ© ‚Üí ‚Ñù) (c : ‚Ñù‚â•0)
    (Œº : Measure Œ© := by volume_tac) [IsFiniteMeasure Œº] : Prop :=
  Kernel.IsSubGaussianWith X c (condExpKernel Œº m) (Œº.trim hm)

def IsCondSubGaussian (X : Œ© ‚Üí ‚Ñù) (Œº : Measure Œ© := by volume_tac) [IsFiniteMeasure Œº] : Prop :=
  ‚àÉ c : ‚Ñù‚â•0, IsCondSubGaussianWith m hm X c Œº

lemma IsCondSubGaussianWith.condExp_le (h : IsCondSubGaussianWith m hm X c Œº) (t : ‚Ñù) :
    ‚àÄ·µê œâ' ‚àÇŒº, (Œº[fun œâ ‚Ü¶ exp (t * X œâ) | m]) œâ' ‚â§ exp (c * t ^ 2 / 2) := by
  have h_eq := condExp_ae_eq_integral_condExpKernel hm (h.integrable_exp_mul t)
  simp_rw [condExpKernel_comp_trim] at h_eq
  filter_upwards [ae_of_ae_trim hm h.mgf_le, h_eq] with œâ' h_mgf h_eq
  rw [h_eq]
  exact h_mgf t

@[simp]
lemma IsCondSubGaussianWith.zero : IsCondSubGaussianWith m hm (fun _ ‚Ü¶ 0) 0 Œº :=
  Kernel.IsSubGaussianWith.zero

@[simp]
lemma IsCondSubGaussianWith.zero' : IsCondSubGaussianWith m hm 0 0 Œº :=
  Kernel.IsSubGaussianWith.zero'

lemma IsCondSubGaussianWith.mem‚Ñíp (h : IsCondSubGaussianWith m hm X c Œº) (t : ‚Ñù) (p : ‚Ñù‚â•0) :
    Mem‚Ñíp (fun œâ ‚Ü¶ exp (t * X œâ)) p Œº :=
  condExpKernel_comp_trim (Œº := Œº) m hm ‚ñ∏ Kernel.IsSubGaussianWith.mem‚Ñíp h t p

lemma IsCondSubGaussianWith.integrable_exp_mul (h : IsCondSubGaussianWith m hm X c Œº) (t : ‚Ñù) :
    Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) Œº :=
  condExpKernel_comp_trim (Œº := Œº) m hm ‚ñ∏ Kernel.IsSubGaussianWith.integrable_exp_mul h t

end Conditional

structure IsSubGaussianWith (X : Œ© ‚Üí ‚Ñù) (c : ‚Ñù‚â•0) (Œº : Measure Œ© := by volume_tac) : Prop where
  integrable_exp_mul : ‚àÄ t : ‚Ñù, Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) Œº
  mgf_le : ‚àÄ t : ‚Ñù, mgf X Œº t ‚â§ exp (c * t ^ 2 / 2)

def IsSubGaussian (X : Œ© ‚Üí ‚Ñù) (Œº : Measure Œ© := by volume_tac) : Prop :=
  ‚àÉ c : ‚Ñù‚â•0, IsSubGaussianWith X c Œº

lemma isSubGaussianWith_iff_kernel :
    IsSubGaussianWith X c Œº
      ‚Üî Kernel.IsSubGaussianWith X c (Kernel.const Unit Œº) (Measure.dirac ()) :=
  ‚ü®fun ‚ü®h1, h2‚ü© ‚Ü¶ ‚ü®by simpa, by simpa‚ü©, fun ‚ü®h1, h2‚ü© ‚Ü¶ ‚ü®by simpa using h1, by simpa using h2‚ü©‚ü©

lemma isSubGaussian_iff_kernel :
    IsSubGaussian X Œº ‚Üî Kernel.IsSubGaussian X (Kernel.const Unit Œº) (Measure.dirac ()) := by
  simp_rw [IsSubGaussian, Kernel.IsSubGaussian, isSubGaussianWith_iff_kernel]

namespace IsSubGaussianWith

lemma aestronglyMeasurable (h : IsSubGaussianWith X c Œº) : AEStronglyMeasurable X Œº := by
  have h_int := h.integrable_exp_mul 1
  simp only [one_mul] at h_int
  exact (aemeasurable_of_aemeasurable_exp h_int.1.aemeasurable).aestronglyMeasurable

lemma mem‚Ñíp (h : IsSubGaussianWith X c Œº) (t : ‚Ñù) (p : ‚Ñù‚â•0) :
    Mem‚Ñíp (fun œâ ‚Ü¶ exp (t * X œâ)) p Œº := by
  rw [isSubGaussianWith_iff_kernel] at h
  simpa using h.mem‚Ñíp t p

lemma cgf_le [SFinite Œº] (h : IsSubGaussianWith X c Œº) (t : ‚Ñù) : cgf X Œº t ‚â§ c * t ^ 2 / 2 := by
  rw [isSubGaussianWith_iff_kernel] at h
  simpa using h.cgf_le t

@[simp]
lemma zero [IsZeroOrProbabilityMeasure Œº] : IsSubGaussianWith (fun _ ‚Ü¶ 0) 0 Œº := by
  simp [isSubGaussianWith_iff_kernel]

@[simp]
lemma zero' [IsZeroOrProbabilityMeasure Œº] : IsSubGaussianWith 0 0 Œº := zero

lemma id_map (hX : AEMeasurable X Œº) :
    IsSubGaussianWith id c (Œº.map X) ‚Üî IsSubGaussianWith X c Œº := by
  refine ‚ü®fun ‚ü®h1, h2‚ü© ‚Ü¶ ‚ü®fun t ‚Ü¶ ?_, ?_‚ü©, fun ‚ü®h1, h2‚ü© ‚Ü¶ ‚ü®fun t ‚Ü¶ ?_, ?_‚ü©‚ü©
  ¬∑ specialize h1 t
    rwa [integrable_map_measure] at h1
    ¬∑ exact Measurable.aestronglyMeasurable <| by fun_prop
    ¬∑ fun_prop
  ¬∑ simpa [Kernel.map_apply _, mgf_id_map hX] using h2
  ¬∑ specialize h1 t
    rwa [integrable_map_measure]
    ¬∑ exact Measurable.aestronglyMeasurable <| by fun_prop
    ¬∑ fun_prop
  ¬∑ simpa [Kernel.map_apply _, mgf_id_map hX] using h2

protected lemma trim (hm : m ‚â§ mŒ©) (hXm : Measurable[m] X) (hX : IsSubGaussianWith X c Œº) :
    IsSubGaussianWith X c (Œº.trim hm) where
  integrable_exp_mul t := by
    refine (hX.integrable_exp_mul t).trim hm ?_
    exact Measurable.stronglyMeasurable <| by fun_prop
  mgf_le t := by
    rw [mgf, ‚Üê integral_trim]
    ¬∑ exact hX.mgf_le t
    ¬∑ exact Measurable.stronglyMeasurable <| by fun_prop

/-- Chernoff bound on the right tail of a sub-Gaussian random variable. -/
lemma measure_ge_le [IsFiniteMeasure Œº] (h : IsSubGaussianWith X c Œº) {Œµ : ‚Ñù}
    (hc : 0 < c) (hŒµ : 0 ‚â§ Œµ) :
    (Œº {œâ | Œµ ‚â§ X œâ}).toReal ‚â§ exp (- Œµ ^ 2 / (2 * c)) := by
  rw [isSubGaussianWith_iff_kernel] at h
  simpa using h.measure_ge_le hc hŒµ

lemma prob_ge_le [IsProbabilityMeasure Œº] (h : IsSubGaussianWith X c Œº) (hŒµ : 0 ‚â§ Œµ) :
    (Œº {œâ | Œµ ‚â§ X œâ}).toReal ‚â§ exp (- Œµ ^ 2 / (2 * c)) := by
  rw [isSubGaussianWith_iff_kernel] at h
  simpa using h.prob_ge_le hŒµ

lemma add_of_indepFun [SFinite Œº] {Y : Œ© ‚Üí ‚Ñù} {cX cY : ‚Ñù‚â•0} (hX : IsSubGaussianWith X cX Œº)
    (hY : IsSubGaussianWith Y cY Œº) (hindep : IndepFun X Y Œº) :
    IsSubGaussianWith (X + Y) (cX + cY) Œº := by
  rw [isSubGaussianWith_iff_kernel] at hX hY ‚ä¢
  simpa using hX.add_of_indepFun hY hindep

lemma sum_of_iIndepFun {Œπ : Type*} [IsZeroOrProbabilityMeasure Œº]
    {X : Œπ ‚Üí Œ© ‚Üí ‚Ñù} (h_indep : iIndepFun (fun _ ‚Ü¶ inferInstance) X Œº) {c : Œπ ‚Üí ‚Ñù‚â•0}
    (h_meas : ‚àÄ i, Measurable (X i))
    {s : Finset Œπ} (h_subG : ‚àÄ i ‚àà s, IsSubGaussianWith (X i) (c i) Œº) :
    IsSubGaussianWith (‚àë i ‚àà s, X i) (‚àë i ‚àà s, c i) Œº := by
  simp_rw [isSubGaussianWith_iff_kernel] at h_subG ‚ä¢
  simpa using Kernel.IsSubGaussianWith.sum_of_iIndepFun h_indep h_meas h_subG

end IsSubGaussianWith

lemma isSubGaussianWith_of_map {Œº : Measure Œ©'} {Y : Œ©' ‚Üí Œ©} {X : Œ© ‚Üí ‚Ñù} (hY : AEMeasurable Y Œº)
    (h : IsSubGaussianWith X c (Œº.map Y)) :
    IsSubGaussianWith (X ‚àò Y) c Œº where
  integrable_exp_mul t := by
    have h1 := h.integrable_exp_mul t
    rwa [integrable_map_measure h1.aestronglyMeasurable (by fun_prop)] at h1
  mgf_le t := by
    convert h.mgf_le t using 1
    rw [mgf_map hY (h.integrable_exp_mul t).1]

section Martingale

lemma isSubGaussianWith_add_of_isCondSubGaussianWith [StandardBorelSpace Œ©] [IsFiniteMeasure Œº]
    {Y : Œ© ‚Üí ‚Ñù} {cY : ‚Ñù‚â•0} (hm : m ‚â§ mŒ©)
    (hX : IsSubGaussianWith X c (Œº.trim hm)) (hY : IsCondSubGaussianWith m hm Y cY Œº) :
    IsSubGaussianWith (X + Y) (c + cY) Œº := by
  suffices IsSubGaussianWith (fun p ‚Ü¶ X p.1 + Y p.2) (c + cY)
      (@Measure.map Œ© (Œ© √ó Œ©) mŒ© (m.prod mŒ©) (fun œâ ‚Ü¶ (id œâ, id œâ)) Œº) by
    have h_eq : X + Y = (fun p ‚Ü¶ X p.1 + Y p.2) ‚àò (fun œâ ‚Ü¶ (id œâ, id œâ)) := by ext; simp
    rw [h_eq]
    refine isSubGaussianWith_of_map ?_ this
    exact @Measurable.aemeasurable _ _ _ (m.prod mŒ©) _ _
      ((measurable_id'' hm).prod_mk measurable_id)
  rw [isSubGaussianWith_iff_kernel] at hX ‚ä¢
  have hY' : Kernel.IsSubGaussianWith Y cY (condExpKernel Œº m)
      (Kernel.const Unit (Œº.trim hm) ‚àò‚Çò Measure.dirac ()) := by simpa
  convert hX.add' hY'
  simp only [id_eq]
  ext
  rw [Kernel.const_apply, ‚Üê Measure.compProd, compProd_trim_condExpKernel]
  rfl

variable {Y : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù} {cY : ‚Ñï ‚Üí ‚Ñù‚â•0} {‚Ñ± : Filtration ‚Ñï mŒ©}

-- In particular, `‚àë i, Y i` is a martingale.
lemma isSubGaussianWith_sum_of_isCondSubGaussianWith [StandardBorelSpace Œ©]
    [IsZeroOrProbabilityMeasure Œº] (h_adapted : Adapted ‚Ñ± Y)
    (h_subG : ‚àÄ i, IsCondSubGaussianWith (‚Ñ± i) (‚Ñ±.le i) (Y i) (cY i) Œº) (n : ‚Ñï) :
    IsSubGaussianWith (fun œâ ‚Ü¶ ‚àë i ‚àà Finset.range n, Y i œâ) (‚àë i ‚àà Finset.range n, cY i) Œº := by
  induction n with
  | zero => simp
  | succ n hn =>
    simp_rw [Finset.sum_range_succ]
    refine isSubGaussianWith_add_of_isCondSubGaussianWith (‚Ñ± n) (‚Ñ±.le n) ?_ (h_subG n)
    refine IsSubGaussianWith.trim (‚Ñ± n) (‚Ñ±.le n) ?_ hn
    exact Finset.measurable_sum (Finset.range n) fun m hm ‚Ü¶
      ((h_adapted m).mono (‚Ñ±.mono (Finset.mem_range_le hm))).measurable

end Martingale

end ProbabilityTheory
