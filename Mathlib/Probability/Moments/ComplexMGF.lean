/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Analysis.Analytic.IsolatedZeros
import Mathlib.Analysis.Calculus.ParametricIntegral
import Mathlib.Analysis.Complex.CauchyIntegral
import Mathlib.Probability.Moments.Basic
import Mathlib.Probability.Moments.IntegrableExpMul

/-!
# The complex-valued moment generating function


## Main definitions

* `complexMGF X Î¼`: the function `z : â„‚ â†¦ Î¼[fun Ï‰ â†¦ cexp (z * X Ï‰)]`.

## Main results

* `complexMGF_ofReal_eq_mgf`: for `x : â„`, `complexMGF X Î¼ x = mgf X Î¼ x`.
* `hasDerivAt_complexMGF`: for all `z : â„‚` such that the real part `z.re` belongs to the interior
  of the interval of definition of the mgf, `complexMGF X Î¼` is differentiable at `z`
  with derivative `Î¼[X * exp (z * X)]`.
* `differentiableOn_complexMGF`: `complexMGF X Î¼` is holomorphic on the vertical strip
  `{z | z.re âˆˆ interior (integrableExpSet X Î¼)}`.
* `analyticOnNhd_complexMGF`: `complexMGF X Î¼` is analytic on the vertical strip
  `{z | z.re âˆˆ interior (integrableExpSet X Î¼)}`.
* `eqOn_complexMGF_of_mgf`: if two random variables have the same moment generating function,
  defined on an interval with nonempty interior, then they have the same `complexMGF`
  on the vertical strip `{z | z.re âˆˆ interior (integrableExpSet X Î¼)}`.

## TODO

Once we have a definition for the characteristic function, we will be able to prove the following.

* `x : â„ â†¦ complexMGF X Î¼ (I * x)` is equal to the characteristic function of
  the random variable `X`.
* As a consequence, if two random variables have same `mgf`, then they have the same
  characteristic function and the same distribution.

-/


open MeasureTheory Filter Finset Real Complex

open scoped MeasureTheory ProbabilityTheory ENNReal NNReal Topology

namespace ProbabilityTheory

variable {Î© Î¹ : Type*} {m : MeasurableSpace Î©} {X : Î© â†’ â„} {p : â„•} {Î¼ : Measure Î©} {t u v : â„}
  {z Îµ : â„‚}

lemma integrable_cexp_iff {Y : Î© â†’ â„‚} (hY : AEMeasurable Y Î¼) :
    Integrable (fun Ï‰ â†¦ cexp (Y Ï‰)) Î¼ â†” Integrable (fun Ï‰ â†¦ rexp ((Y Ï‰).re)) Î¼ := by
  rw [â† integrable_norm_iff]
  swap; Â· exact (Complex.measurable_exp.comp_aemeasurable hY).aestronglyMeasurable
  congr! with Ï‰
  simp only [Complex.norm_eq_abs, Complex.abs_exp]

/-- Complex extension of the moment generating function. -/
noncomputable
def complexMGF (X : Î© â†’ â„) (Î¼ : Measure Î©) (z : â„‚) : â„‚ := Î¼[fun Ï‰ â†¦ cexp (z * X Ï‰)]

lemma complexMGF_undef (hX : AEMeasurable X Î¼) (h : Â¬ Integrable (fun Ï‰ â†¦ rexp (z.re * X Ï‰)) Î¼) :
    complexMGF X Î¼ z = 0 := by
  rw [complexMGF, integral_undef]
  rw [integrable_cexp_iff]
  Â· simpa using h
  Â· exact (Complex.measurable_ofReal.comp_aemeasurable hX).const_mul _

lemma abs_complexMGF_le_mgf : abs (complexMGF X Î¼ z) â‰¤ mgf X Î¼ z.re := by
  rw [complexMGF, â† re_add_im z]
  simp_rw [add_mul, Complex.exp_add, re_add_im, â† Complex.norm_eq_abs]
  calc â€–âˆ« Ï‰, cexp (z.re * X Ï‰) * cexp (z.im * I * X Ï‰) âˆ‚Î¼â€–
  _ â‰¤ âˆ« Ï‰, â€–cexp (z.re * X Ï‰) * cexp (z.im * I * X Ï‰)â€– âˆ‚Î¼ := norm_integral_le_integral_norm _
  _ = âˆ« Ï‰, abs (cexp (z.re * X Ï‰)) âˆ‚Î¼ := by
    simp only [norm_mul, Complex.norm_eq_abs]
    congr with Ï‰
    simp only [ne_eq, map_eq_zero, Complex.exp_ne_zero, not_false_eq_true, mul_eq_leftâ‚€]
    rw [mul_comm _ I, mul_assoc, mul_comm]
    exact mod_cast abs_exp_ofReal_mul_I _
  _ = âˆ« Ï‰, rexp (z.re * X Ï‰) âˆ‚Î¼ := by simp [Complex.abs_exp]

lemma complexMGF_ofReal_eq_mgf (x : â„) : complexMGF X Î¼ x = mgf X Î¼ x := by
  rw [complexMGF]
  norm_cast
  have : âˆ« Ï‰, (rexp (x * X Ï‰) : â„‚) âˆ‚Î¼ = âˆ« Ï‰, rexp (x * X Ï‰) âˆ‚Î¼ := integral_ofReal
  rw [this]
  simp only [mgf]

lemma re_complexMGF_ofReal (x : â„) : (complexMGF X Î¼ x).re = mgf X Î¼ x := by
  simp [complexMGF_ofReal_eq_mgf]

lemma re_complexMGF_ofReal' : (fun x : â„ â†¦ (complexMGF X Î¼ x).re) = mgf X Î¼ := by
  ext x
  exact re_complexMGF_ofReal x

lemma convexMGF_add_sub_range (ht : t â‰  0)
    (h_int_pos : Integrable (fun Ï‰ â†¦ rexp ((z.re + t) * X Ï‰)) Î¼)
    (h_int_neg : Integrable (fun Ï‰ â†¦ rexp ((z.re - t) * X Ï‰)) Î¼)
    (hÎµ : |Îµ.re| â‰¤ |t|) (n : â„•) :
    complexMGF X Î¼ (z + Îµ) - âˆ‘ m in range n, Îµ ^ m / m.factorial * âˆ« Ï‰, X Ï‰ ^ m * cexp (z * X Ï‰) âˆ‚Î¼
      = Î¼[fun Ï‰ â†¦ cexp (z * X Ï‰)
        * (cexp (Îµ * X Ï‰) - âˆ‘ m in range n, Îµ ^ m / m.factorial * X Ï‰ ^ m)] := by
  have hX : AEMeasurable X Î¼ := aemeasurable_of_integrable_exp_mul ?_ h_int_pos h_int_neg
  swap; Â· rw [â† sub_ne_zero]; simp [ht]
  have hÎµ_int_pos : Integrable (fun Ï‰ â†¦ rexp ((z.re + Îµ.re) * X Ï‰)) Î¼ := by
    refine integrable_exp_mul_of_le_of_le (a := z.re - |t|) (b := z.re + |t|) ?_ ?_ ?_ ?_
    Â· rcases le_total 0 t with ht | ht
      Â· rwa [_root_.abs_of_nonneg ht]
      Â· simpa [abs_of_nonpos ht]
    Â· rcases le_total 0 t with ht | ht
      Â· rwa [_root_.abs_of_nonneg ht]
      Â· rwa [abs_of_nonpos ht]
    Â· rw [sub_eq_add_neg]
      gcongr
      rw [neg_le]
      exact (neg_le_abs _).trans hÎµ
    Â· gcongr
      exact (le_abs_self _).trans hÎµ
  have h_int_zÎµ : Integrable (fun Ï‰ â†¦ cexp ((z + Îµ) * X Ï‰)) Î¼ := by
    rw [integrable_cexp_iff]
    swap; Â· exact (Complex.measurable_ofReal.comp_aemeasurable hX).const_mul _
    simp only [mul_re, add_re, ofReal_re, add_im, ofReal_im, mul_zero, sub_zero]
    exact hÎµ_int_pos
  have h_int_mul i : Integrable (fun Ï‰ â†¦ X Ï‰ ^ i * cexp (z * X Ï‰)) Î¼ := by
    rw [â† integrable_norm_iff]
    swap
    Â· refine AEMeasurable.aestronglyMeasurable ?_
      refine ((Complex.measurable_ofReal.comp_aemeasurable hX).pow_const _).mul ?_
      refine Complex.measurable_exp.comp_aemeasurable ?_
      exact (Complex.measurable_ofReal.comp_aemeasurable hX).const_mul _
    simp only [norm_mul, Complex.norm_eq_abs, abs_ofReal, Complex.abs_exp, mul_re, ofReal_re,
      ofReal_im, mul_zero, sub_zero]
    convert integrable_pow_abs_mul_exp_of_integrable_exp_mul ht h_int_pos h_int_neg i
    simp
  simp_rw [complexMGF, add_mul, Complex.exp_add, mul_comm _ (cexp (Îµ * X _))]
  calc âˆ« Ï‰, cexp (Îµ * X Ï‰) * cexp (z * X Ï‰) âˆ‚Î¼ -
      âˆ‘ m âˆˆ range n, Îµ ^ m / m.factorial * âˆ« Ï‰, X Ï‰ ^ m * cexp (z * X Ï‰) âˆ‚Î¼
  _ = âˆ« Ï‰, cexp (Îµ * X Ï‰) * cexp (z * X Ï‰) âˆ‚Î¼ -
      âˆ‘ m âˆˆ range n, âˆ« Ï‰, Îµ ^ m / m.factorial * X Ï‰ ^ m * cexp (z * X Ï‰) âˆ‚Î¼ := by
    congr with m
    rw [â† integral_mul_left]
    simp_rw [mul_assoc]
  _ = âˆ« Ï‰, cexp (Îµ * X Ï‰) * cexp (z * X Ï‰) âˆ‚Î¼ -
      âˆ« Ï‰, âˆ‘ m âˆˆ range n, Îµ ^ m / m.factorial * X Ï‰ ^ m * cexp (z * X Ï‰) âˆ‚Î¼ := by
    congr
    rw [integral_finset_sum _ fun i hi â†¦ ?_]
    simp_rw [mul_assoc]
    refine Integrable.const_mul ?_ _
    exact h_int_mul _
  _ = âˆ« Ï‰, cexp (z * X Ï‰) * (cexp (Îµ * X Ï‰) - âˆ‘ m âˆˆ range n, Îµ ^ m / m.factorial * X Ï‰ ^ m) âˆ‚Î¼ := by
    rw [â† integral_sub]
    Â· congr with Ï‰
      simp_rw [mul_sub]
      congr 1
      Â· rw [mul_comm]
      Â· rw [mul_sum]
        congr with m
        ring
    Â· simp_rw [â† Complex.exp_add, â† add_mul, add_comm Îµ]
      exact h_int_zÎµ
    Â· refine integrable_finset_sum _ fun m hm â†¦ ?_
      simp_rw [mul_assoc]
      refine Integrable.const_mul ?_ _
      exact h_int_mul _

lemma abs_exp_sub_range_le_exp_sub_range_abs (x : â„‚) (n : â„•) :
  abs (cexp x - âˆ‘ m âˆˆ range n, x ^ m / m.factorial)
    â‰¤ rexp (abs x) - âˆ‘ m âˆˆ range n, (abs x) ^ m / m.factorial := by
  rw [â† CauSeq.lim_const (abv := Complex.abs) (âˆ‘ m âˆˆ range n, _), Complex.exp, sub_eq_add_neg,
    â† CauSeq.lim_neg, CauSeq.lim_add, â† lim_abs]
  refine CauSeq.lim_le (CauSeq.le_of_exists âŸ¨n, fun j hj => ?_âŸ©)
  simp_rw [â† sub_eq_add_neg]
  calc abs ((âˆ‘ m âˆˆ range j, x ^ m / m.factorial) - âˆ‘ m âˆˆ range n, x ^ m / m.factorial)
  _ â‰¤ (âˆ‘ m âˆˆ range j, abs x ^ m / m.factorial) - âˆ‘ m âˆˆ range n, abs x ^ m / m.factorial := by
    rw [sum_range_sub_sum_range hj, sum_range_sub_sum_range hj]
    refine (IsAbsoluteValue.abv_sum Complex.abs ..).trans_eq ?_
    congr with i
    simp
  _ â‰¤ rexp (abs x) - âˆ‘ m âˆˆ range n, (abs x) ^ m / m.factorial := by
    gcongr
    exact sum_le_exp_of_nonneg (by exact AbsoluteValue.nonneg abs x) _

lemma abs_exp_le_exp_abs (z : â„‚) : abs (cexp z) â‰¤ rexp (abs z) := by
  convert abs_exp_sub_range_le_exp_sub_range_abs z 0 using 1 <;> simp

lemma exp_bound_exp (x : â„‚) (n : â„•) :
    abs (cexp x - âˆ‘ m âˆˆ range n, x ^ m / m.factorial) â‰¤ abs x ^ n * rexp (abs x) := by
  rw [â† CauSeq.lim_const (abv := Complex.abs) (âˆ‘ m âˆˆ range n, _), Complex.exp, sub_eq_add_neg,
    â† CauSeq.lim_neg, CauSeq.lim_add, â† lim_abs]
  refine CauSeq.lim_le (CauSeq.le_of_exists âŸ¨n, fun j hj => ?_âŸ©)
  simp_rw [â† sub_eq_add_neg]
  show abs ((âˆ‘ m âˆˆ range j, x ^ m / m.factorial) - âˆ‘ m âˆˆ range n, x ^ m / m.factorial) â‰¤ _
  rw [sum_range_sub_sum_range hj]
  calc
    abs (âˆ‘ m âˆˆ range j with n â‰¤ m, (x ^ m / m.factorial : â„‚))
      = abs (âˆ‘ m âˆˆ range j with n â‰¤ m, (x ^ n * (x ^ (m - n) / m.factorial) : â„‚)) := by
      refine congr_arg abs (sum_congr rfl fun m hm => ?_)
      rw [mem_filter, mem_range] at hm
      rw [â† mul_div_assoc, â† pow_add, add_tsub_cancel_of_le hm.2]
    _ â‰¤ âˆ‘ m âˆˆ range j with n â‰¤ m, abs (x ^ n * (x ^ (m - n) / m.factorial)) :=
      IsAbsoluteValue.abv_sum Complex.abs ..
    _ â‰¤ âˆ‘ m âˆˆ range j with n â‰¤ m, abs x ^ n * (abs x ^ (m - n) / (m - n).factorial) := by
      simp_rw [map_mul, map_pow, map_divâ‚€, abs_natCast]
      gcongr with i hi
      Â· rw [IsAbsoluteValue.abv_pow abs]
      Â· simp
    _ = abs x ^ n * âˆ‘ m âˆˆ range j with n â‰¤ m, (abs x ^ (m - n) / (m - n).factorial) := by
      rw [â† mul_sum]
    _ = abs x ^ n * âˆ‘ m âˆˆ range (j - n), (abs x ^ m / m.factorial) := by
      congr 1
      refine (sum_bij (fun m hm â†¦ m + n) ?_ ?_ ?_ ?_).symm
      Â· intro a ha
        simp only [mem_filter, mem_range, le_add_iff_nonneg_left, zero_le, and_true]
        simp only [mem_range] at ha
        rwa [â† lt_tsub_iff_right]
      Â· intro a ha b hb hab
        simpa using hab
      Â· intro b hb
        simp only [mem_range, exists_prop]
        simp only [mem_filter, mem_range] at hb
        refine âŸ¨b - n, ?_, ?_âŸ©
        Â· rw [tsub_lt_tsub_iff_right hb.2]
          exact hb.1
        Â· rw [tsub_add_cancel_of_le hb.2]
      Â· simp
    _ â‰¤ abs x ^ n * rexp (abs x) := by
      gcongr
      refine sum_le_exp_of_nonneg ?_ _
      exact AbsoluteValue.nonneg abs x

lemma abs_convexMGF_add_sub_range_le
    (h_int_pos : Integrable (fun Ï‰ â†¦ rexp ((z.re + t) * X Ï‰)) Î¼)
    (h_int_neg : Integrable (fun Ï‰ â†¦ rexp ((z.re - t) * X Ï‰)) Î¼)
    (hÎµ : abs Îµ < |t|) (n : â„•):
    abs (complexMGF X Î¼ (z + Îµ)
        - âˆ‘ m in range n, Îµ ^ m / m.factorial * âˆ« Ï‰, X Ï‰ ^ m * cexp (z * X Ï‰) âˆ‚Î¼)
      â‰¤ (abs Îµ) ^ n * Î¼[fun Ï‰ â†¦ |X Ï‰| ^ n * rexp (z.re * X Ï‰ + abs Îµ * |X Ï‰|)] := by
  have ht : t â‰  0 := by
    suffices |t| â‰  0 by simpa
    refine (lt_of_le_of_lt ?_ hÎµ).ne'
    exact AbsoluteValue.nonneg abs Îµ
  rw [convexMGF_add_sub_range ht h_int_pos h_int_neg ((abs_re_le_abs Îµ).trans hÎµ.le),
    â† integral_mul_left, â† Complex.norm_eq_abs]
  refine (norm_integral_le_integral_norm _).trans ?_
  simp only [norm_mul, Complex.norm_eq_abs, Complex.abs_exp, mul_re, ofReal_re, ofReal_im, mul_zero,
    sub_zero, _root_.sq_abs]
  refine integral_mono_of_nonneg (ae_of_all _ fun Ï‰ â†¦ ?_) ?_ (ae_of_all _ fun Ï‰ â†¦ ?_)
  Â· positivity
  Â· refine Integrable.const_mul ?_ _
    exact integrable_pow_abs_mul_exp_add_of_integrable_exp_mul h_int_pos h_int_neg
      (AbsoluteValue.nonneg abs Îµ) hÎµ n
  Â· simp_rw [Real.exp_add, mul_comm (rexp (z.re * X Ï‰)), â† mul_assoc]
    gcongr
    convert exp_bound_exp (Îµ * X Ï‰) n using 4 with m hm
    Â· rw [mul_pow]
      ring
    Â· simp [mul_pow]
    Â· simp

lemma tendsto_todo_pow_abs (hz : z.re âˆˆ interior (integrableExpSet X Î¼)) (n : â„•) :
    Tendsto (fun h â†¦ Î¼[fun Ï‰ â†¦ |X Ï‰| ^ n * rexp (z.re * X Ï‰ + abs h * |X Ï‰|)]) (ğ“ 0)
      (ğ“ Î¼[fun Ï‰ â†¦ |X Ï‰| ^ n * rexp (z.re * X Ï‰)]) := by
  have hX : AEMeasurable X Î¼ := aemeasurable_of_mem_interior_integrableExpSet hz
  rw [mem_interior_iff_mem_nhds, mem_nhds_iff_exists_Ioo_subset] at hz
  obtain âŸ¨l, u, hlu, h_subsetâŸ© := hz
  let t := ((z.re - l) âŠ“ (u - z.re)) / 2
  have h_pos : 0 < (z.re - l) âŠ“ (u - z.re) := by simp [hlu.1, hlu.2]
  have ht : 0 < t := half_pos h_pos
  refine tendsto_integral_filter_of_dominated_convergence
    (fun Ï‰ â†¦ |X Ï‰| ^ n * rexp (z.re * X Ï‰ + t/2 * |X Ï‰|)) ?_ ?_ ?_ ?_
  Â· refine .of_forall fun h â†¦ AEMeasurable.aestronglyMeasurable ?_
    exact AEMeasurable.mul (by fun_prop) (Real.measurable_exp.comp_aemeasurable (by fun_prop))
  Â· rw [eventually_nhds_iff]
    refine âŸ¨{x | abs x < t/2}, fun y hy â†¦ ?_, ?_, by simp [ht]âŸ©
    Â· refine ae_of_all _ fun Ï‰ â†¦ ?_
      simp only [norm_mul, norm_pow, Real.norm_eq_abs, _root_.abs_abs, Real.abs_exp]
      gcongr
      exact hy.le
    Â· exact isOpen_lt Complex.continuous_abs (by fun_prop)
  Â· convert integrable_pow_abs_mul_exp_add_of_integrable_exp_mul ?_ ?_ (half_pos ht).le (t := t)
      ?_ n using 3
    Â· exact h_subset (add_half_inf_sub_mem_Ioo hlu)
    Â· exact h_subset (sub_half_inf_sub_mem_Ioo hlu)
    Â· simp [_root_.abs_of_nonneg ht.le, ht]
  Â· refine ae_of_all _ fun Ï‰ â†¦ ?_
    refine Tendsto.const_mul _ ?_
    refine (Real.continuous_exp.tendsto _).comp ?_
    nth_rw 2 [â† add_zero (z.re * X Ï‰)]
    refine tendsto_const_nhds.add ?_
    rw [â† zero_mul (|X Ï‰|)]
    refine Tendsto.mul_const _ ?_
    convert continuous_abs.tendsto 0
    simp

lemma isBigO_abs_convexMGF_add_sub_range (hz : z.re âˆˆ interior (integrableExpSet X Î¼)) (n : â„•) :
    (fun Îµ â†¦ complexMGF X Î¼ (z + Îµ)
        - âˆ‘ m in range n, Îµ ^ m / m.factorial * âˆ« Ï‰, X Ï‰ ^ m * cexp (z * X Ï‰) âˆ‚Î¼)
      =O[ğ“ 0] fun Îµ â†¦ (abs Îµ) ^ n := by
  let hz' := hz
  rw [mem_interior_iff_mem_nhds, mem_nhds_iff_exists_Ioo_subset] at hz'
  obtain âŸ¨l, u, hlu, h_subsetâŸ© := hz'
  let t := ((z.re - l) âŠ“ (u - z.re)) / 2
  have h_pos : 0 < (z.re - l) âŠ“ (u - z.re) := by simp [hlu.1, hlu.2]
  have ht : 0 < t := half_pos h_pos
  calc
  _ =O[ğ“ 0] fun Îµ : â„‚ â†¦ (abs Îµ) ^ n * Î¼[fun Ï‰ â†¦ |X Ï‰| ^ n * rexp (z.re * X Ï‰ + abs Îµ * |X Ï‰|)] := by
    refine Eventually.isBigO ?_
    rw [eventually_nhds_iff]
    refine âŸ¨{x | abs x < t}, fun y hy â†¦ ?_, ?_, by simp [ht]âŸ©
    Â· simp only [Real.norm_eq_abs, Complex.abs_abs]
      refine abs_convexMGF_add_sub_range_le ?_ ?_ (hy.trans_le (le_abs_self _)) n
      Â· exact h_subset (add_half_inf_sub_mem_Ioo hlu)
      Â· exact h_subset (sub_half_inf_sub_mem_Ioo hlu)
    Â· refine isOpen_lt ?_ (by fun_prop)
      exact Complex.continuous_abs -- fun_prop fails
  _ =O[ğ“ 0] fun Îµ â†¦ (abs Îµ) ^ n * 1 := by
    refine Asymptotics.IsBigO.mul (Asymptotics.isBigO_refl _ _) ?_
    refine Tendsto.isBigO_one _ (c := Î¼[fun Ï‰ â†¦ |X Ï‰| ^ n * rexp (z.re * X Ï‰)]) ?_
    exact tendsto_todo_pow_abs hz n
  _ = fun Îµ â†¦ (abs Îµ) ^ n := by simp

/-- For all `z : â„‚` such that the real part `z.re` belongs to the interior
  of the interval of definition of the mgf, `complexMGF X Î¼` is differentiable at `z`
  with derivative `Î¼[X * exp (z * X)]`. -/
theorem hasDerivAt_complexMGF (hz : z.re âˆˆ interior (integrableExpSet X Î¼)) :
    HasDerivAt (complexMGF X Î¼) Î¼[fun Ï‰ â†¦ X Ï‰ * cexp (z * X Ï‰)] z := by
  rw [hasDerivAt_iff_isLittleO_nhds_zero]
  simp only [smul_eq_mul]
  calc (fun h â†¦ complexMGF X Î¼ (z + h) - complexMGF X Î¼ z - h * âˆ« Ï‰, X Ï‰ * cexp (z * X Ï‰) âˆ‚Î¼)
  _ =O[ğ“ 0] fun h â†¦ (abs h)^2 := by
    convert isBigO_abs_convexMGF_add_sub_range hz 2 using 2
    simp [sum_range, sub_add_eq_sub_sub, complexMGF]
  _ =o[ğ“ 0] fun h â†¦ h := Asymptotics.isLittleO_norm_pow_id one_lt_two

/-- `complexMGF X Î¼` is holomorphic on the vertical strip
`{z | z.re âˆˆ interior (integrableExpSet X Î¼)}`. -/
theorem differentiableOn_complexMGF :
    DifferentiableOn â„‚ (complexMGF X Î¼) {z | z.re âˆˆ interior (integrableExpSet X Î¼)} := by
  intro z hz
  have h := hasDerivAt_complexMGF hz
  rw [hasDerivAt_iff_hasFDerivAt] at h
  exact h.hasFDerivWithinAt.differentiableWithinAt

/-- `complexMGF X Î¼` is analytic on the vertical strip
  `{z | z.re âˆˆ interior (integrableExpSet X Î¼)}`. -/
theorem analyticOnNhd_complexMGF :
    AnalyticOnNhd â„‚ (complexMGF X Î¼) {z | z.re âˆˆ interior (integrableExpSet X Î¼)} :=
  differentiableOn_complexMGF.analyticOnNhd (isOpen_interior.preimage Complex.continuous_re)

lemma analyticAt_complexMGF (hz : z.re âˆˆ interior (integrableExpSet X Î¼)) :
    AnalyticAt â„‚ (complexMGF X Î¼) z := analyticOnNhd_complexMGF z hz

lemma hasDerivAt_integral_pow_mul_exp (hz : z.re âˆˆ interior (integrableExpSet X Î¼)) (n : â„•) :
    HasDerivAt (fun z â†¦ Î¼[fun Ï‰ â†¦ X Ï‰ ^ n * cexp (z * X Ï‰)])
        Î¼[fun Ï‰ â†¦ X Ï‰ ^ (n + 1) * cexp (z * X Ï‰)] z := by
  have hX : AEMeasurable X Î¼ := aemeasurable_of_mem_interior_integrableExpSet hz
  let hz' := hz
  rw [mem_interior_iff_mem_nhds, mem_nhds_iff_exists_Ioo_subset] at hz
  obtain âŸ¨l, u, hlu, h_subsetâŸ© := hz
  let t := ((z.re - l) âŠ“ (u - z.re)) / 2
  have h_pos : 0 < (z.re - l) âŠ“ (u - z.re) := by simp [hlu.1, hlu.2]
  have ht : 0 < t := half_pos h_pos
  refine (hasDerivAt_integral_of_dominated_loc_of_deriv_le
    (bound := fun Ï‰ â†¦ |X Ï‰| ^ (n + 1) * rexp (z.re * X Ï‰ + t/2 * |X Ï‰|))
    (F := fun z Ï‰ â†¦ X Ï‰ ^ n * cexp (z * X Ï‰))
    (F' := fun z Ï‰ â†¦ X Ï‰ ^ (n + 1) * cexp (z * X Ï‰)) (half_pos ht) ?_ ?_ ?_ ?_ ?_ ?_).2
  Â· refine .of_forall fun z â†¦ AEMeasurable.aestronglyMeasurable ?_
    exact AEMeasurable.mul (by fun_prop) (Complex.measurable_exp.comp_aemeasurable (by fun_prop))
  Â· rw [â† integrable_norm_iff]
    swap
    Â· refine AEMeasurable.aestronglyMeasurable ?_
      refine ((Complex.measurable_ofReal.comp_aemeasurable hX).pow_const _).mul ?_
      refine Complex.measurable_exp.comp_aemeasurable ?_
      exact (Complex.measurable_ofReal.comp_aemeasurable hX).const_mul _
    simp only [norm_mul, Complex.norm_eq_abs, abs_ofReal, Complex.abs_exp, mul_re, ofReal_re,
      ofReal_im, mul_zero, sub_zero]
    convert integrable_pow_abs_mul_exp_of_mem_interior_integrableExpSet hz n
    simp
  Â· refine AEMeasurable.aestronglyMeasurable ?_
    exact AEMeasurable.mul (by fun_prop) (Complex.measurable_exp.comp_aemeasurable (by fun_prop))
  Â· refine ae_of_all _ fun Ï‰ Îµ hÎµ â†¦ ?_
    simp only [norm_mul, norm_pow, norm_real, Real.norm_eq_abs, Complex.norm_eq_abs]
    rw [Complex.abs_ofReal, Complex.abs_exp]
    simp only [mul_re, ofReal_re, ofReal_im, mul_zero, sub_zero]
    gcongr
    have : Îµ = z + (Îµ - z) := by simp
    rw [this, add_re, add_mul]
    gcongr _ + ?_
    refine (le_abs_self _).trans ?_
    rw [abs_mul]
    gcongr
    refine (abs_re_le_abs _).trans ?_
    simp only [Metric.mem_ball, dist_eq_norm, Complex.norm_eq_abs] at hÎµ
    exact hÎµ.le
  Â· refine integrable_pow_abs_mul_exp_add_of_integrable_exp_mul ?_ ?_ ?_ ?_ (t := t) (n + 1)
    Â· exact h_subset (add_half_inf_sub_mem_Ioo hlu)
    Â· exact h_subset (sub_half_inf_sub_mem_Ioo hlu)
    Â· positivity
    Â· refine lt_of_lt_of_le ?_ (le_abs_self _)
      simp [ht]
  Â· refine ae_of_all _ fun Ï‰ Îµ hÎµ â†¦ ?_
    simp only
    simp_rw [pow_succ, mul_assoc]
    refine HasDerivAt.const_mul _ ?_
    simp_rw [â† smul_eq_mul, Complex.exp_eq_exp_â„‚]
    convert hasDerivAt_exp_smul_const (X Ï‰ : â„‚) Îµ using 1
    rw [smul_eq_mul, mul_comm]

lemma hasDerivAt_integral_pow_mul_exp_real (ht : t âˆˆ interior (integrableExpSet X Î¼)) (n : â„•) :
    HasDerivAt (fun t â†¦ Î¼[fun Ï‰ â†¦ X Ï‰ ^ n * rexp (t * X Ï‰)])
      Î¼[fun Ï‰ â†¦ X Ï‰ ^ (n + 1) * rexp (t * X Ï‰)] t := by
  have hX : AEMeasurable X Î¼ := aemeasurable_of_mem_interior_integrableExpSet ht
  have h_re_of_mem n t (ht' : t âˆˆ interior (integrableExpSet X Î¼)) :
      (âˆ« Ï‰, X Ï‰ ^ n * cexp (t * X Ï‰) âˆ‚Î¼).re = âˆ« Ï‰, X Ï‰ ^ n * rexp (t * X Ï‰) âˆ‚Î¼ := by
    simp_rw [â† RCLike.re_eq_complex_re]
    rw [â† integral_re]
    Â· norm_cast
    Â· rw [â† integrable_norm_iff]
      swap
      Â· refine AEMeasurable.aestronglyMeasurable ?_
        refine ((Complex.measurable_ofReal.comp_aemeasurable hX).pow_const _).mul ?_
        refine Complex.measurable_exp.comp_aemeasurable ?_
        exact (Complex.measurable_ofReal.comp_aemeasurable hX).const_mul _
      simp only [norm_mul, Complex.norm_eq_abs, abs_ofReal, Complex.abs_exp, mul_re, ofReal_re,
        ofReal_im, mul_zero, sub_zero, Complex.abs_pow]
      exact integrable_pow_abs_mul_exp_of_mem_interior_integrableExpSet ht' n
  have h_re n : âˆ€á¶  t' : â„ in ğ“ t, (âˆ« Ï‰, X Ï‰ ^ n * cexp (t' * X Ï‰) âˆ‚Î¼).re
      = âˆ« Ï‰, X Ï‰ ^ n * rexp (t' * X Ï‰) âˆ‚Î¼ := by
    filter_upwards [isOpen_interior.eventually_mem ht] with t ht' using h_re_of_mem n t ht'
  rw [â† EventuallyEq.hasDerivAt_iff (h_re _), â† h_re_of_mem _ t ht]
  have h := hasDerivAt_integral_pow_mul_exp (X := X) (Î¼ := Î¼) (z := t) ?_ n
  swap; Â· simp [ht]
  exact h.real_of_complex

lemma hasDeriAt_iteratedDeriv_complexMGF (hz : z.re âˆˆ interior (integrableExpSet X Î¼)) (n : â„•) :
    HasDerivAt (iteratedDeriv n (complexMGF X Î¼)) Î¼[fun Ï‰ â†¦ X Ï‰ ^ (n + 1) * cexp (z * X Ï‰)] z := by
  induction n generalizing z with
  | zero => simp [hasDerivAt_complexMGF hz]
  | succ n hn =>
    rw [iteratedDeriv_succ]
    have : deriv (iteratedDeriv n (complexMGF X Î¼))
        =á¶ [ğ“ z] fun z â†¦ Î¼[fun Ï‰ â†¦ X Ï‰ ^ (n + 1) * cexp (z * X Ï‰)] := by
      have h_mem : âˆ€á¶  y in ğ“ z, y.re âˆˆ interior (integrableExpSet X Î¼) := by
        refine IsOpen.eventually_mem ?_ hz
        exact isOpen_interior.preimage Complex.continuous_re
      filter_upwards [h_mem] with y hy using HasDerivAt.deriv (hn hy)
    rw [EventuallyEq.hasDerivAt_iff this]
    exact hasDerivAt_integral_pow_mul_exp hz (n + 1)

lemma iteratedDeriv_complexMGF (hz : z.re âˆˆ interior (integrableExpSet X Î¼)) (n : â„•) :
    iteratedDeriv n (complexMGF X Î¼) z = Î¼[fun Ï‰ â†¦ X Ï‰ ^ n * cexp (z * X Ï‰)] := by
  induction n generalizing z with
  | zero => simp [complexMGF]
  | succ n hn =>
    rw [iteratedDeriv_succ]
    exact (hasDeriAt_iteratedDeriv_complexMGF hz n).deriv

lemma integrableExpSet_eq_of_mgf {Y : Î© â†’ â„} (hXY : mgf X Î¼ = mgf Y Î¼) :
    integrableExpSet X Î¼ = integrableExpSet Y Î¼ := by
  ext t
  simp only [integrableExpSet, Set.mem_setOf_eq]
  by_cases hÎ¼ : Î¼ = 0
  Â· simp [hÎ¼]
  rw [â† mgf_pos_iff' hÎ¼, â† mgf_pos_iff' hÎ¼, hXY]

/-- If two random variables have the same moment generating function, defined on an interval with
nonempty interior, then they have the same `complexMGF` on the vertical strip
`{z | z.re âˆˆ interior (integrableExpSet X Î¼)}`. -/
lemma eqOn_complexMGF_of_mgf {Y : Î© â†’ â„} (hXY : mgf X Î¼ = mgf Y Î¼)
    (ht : t âˆˆ interior (integrableExpSet X Î¼)) :
    Set.EqOn (complexMGF X Î¼) (complexMGF Y Î¼)
      {z | z.re âˆˆ interior (integrableExpSet X Î¼)} := by
  have hX : AnalyticOnNhd â„‚ (complexMGF X Î¼) {z | z.re âˆˆ interior (integrableExpSet X Î¼)} :=
    analyticOnNhd_complexMGF
  have hY : AnalyticOnNhd â„‚ (complexMGF Y Î¼) {z | z.re âˆˆ interior (integrableExpSet Y Î¼)} :=
    analyticOnNhd_complexMGF
  rw [integrableExpSet_eq_of_mgf hXY] at hX ht âŠ¢
  refine AnalyticOnNhd.eqOn_of_preconnected_of_frequently_eq hX hY ?_ (zâ‚€ := (t : â„‚))
    (by simp [ht]) ?_
  Â· exact (convex_integrableExpSet.interior.linear_preimage reLm).isPreconnected
  Â· have h_real : âˆƒá¶  (x : â„) in ğ“[â‰ ] t, complexMGF X Î¼ x = complexMGF Y Î¼ x := by
      refine .of_forall fun y â†¦ ?_
      rw [complexMGF_ofReal_eq_mgf, complexMGF_ofReal_eq_mgf, hXY]
    rw [frequently_iff_seq_forall] at h_real âŠ¢
    obtain âŸ¨xs, hx_tendsto, hx_eqâŸ© := h_real
    refine âŸ¨fun n â†¦ xs n, ?_, fun n â†¦ ?_âŸ©
    Â· rw [tendsto_nhdsWithin_iff] at hx_tendsto âŠ¢
      constructor
      Â· rw [tendsto_ofReal_iff]
        exact hx_tendsto.1
      Â· simpa using hx_tendsto.2
    Â· simp [hx_eq]

end ProbabilityTheory
