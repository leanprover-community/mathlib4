/-
Copyright (c) 2025 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import Mathlib.Analysis.Analytic.IsolatedZeros
import Mathlib.Analysis.Calculus.ParametricIntegral
import Mathlib.Analysis.Complex.CauchyIntegral
import Mathlib.Probability.Moments.Basic
import Mathlib.Probability.Moments.IntegrableExpMul

/-!
# The complex-valued moment generating function

The moment generating function (mgf) is `t : ‚Ñù ‚Ü¶ Œº[fun œâ ‚Ü¶ rexp (t * X œâ)]`. It can be extended to
a complex function `z : ‚ÑÇ ‚Ü¶ Œº[fun œâ ‚Ü¶ cexp (z * X œâ)]`, which we call `complexMGF X Œº`.
That function is holomorphic on the vertical strip with base the interior of the interval
of definition of the mgf.
On the vertical line that goes through 0, `complexMGF X Œº` is equal to the characteristic function.
This allows us to link properties of the characteristic function and the mgf (mostly deducing
properties of the mgf from those of the characteristic function).

## Main definitions

* `complexMGF X Œº`: the function `z : ‚ÑÇ ‚Ü¶ Œº[fun œâ ‚Ü¶ cexp (z * X œâ)]`.

## Main results

* `complexMGF_ofReal`: for `x : ‚Ñù`, `complexMGF X Œº x = mgf X Œº x`.

* `hasDerivAt_complexMGF`: for all `z : ‚ÑÇ` such that the real part `z.re` belongs to the interior
  of the interval of definition of the mgf, `complexMGF X Œº` is differentiable at `z`
  with derivative `Œº[X * exp (z * X)]`.
* `differentiableOn_complexMGF`: `complexMGF X Œº` is holomorphic on the vertical strip
  `{z | z.re ‚àà interior (integrableExpSet X Œº)}`.
* `analyticOnNhd_complexMGF`: `complexMGF X Œº` is analytic on the vertical strip
  `{z | z.re ‚àà interior (integrableExpSet X Œº)}`.

* `eqOn_complexMGF_of_mgf`: if two random variables have the same moment generating function,
  then they have the same `complexMGF` on the vertical strip
  `{z | z.re ‚àà interior (integrableExpSet X Œº)}`.

## TODO

Once we have a definition for the characteristic function, we will be able to prove the following.

* `x : ‚Ñù ‚Ü¶ complexMGF X Œº (I * x)` is equal to the characteristic function of
  the random variable `X`.
* As a consequence, if two random variables have same `mgf`, then they have the same
  characteristic function and the same distribution.

-/


open MeasureTheory Filter Finset Real Complex

open scoped MeasureTheory ProbabilityTheory ENNReal NNReal Topology

namespace ProbabilityTheory

variable {Œ© Œπ : Type*} {m : MeasurableSpace Œ©} {X : Œ© ‚Üí ‚Ñù} {p : ‚Ñï} {Œº : Measure Œ©} {t u v : ‚Ñù}
  {z Œµ : ‚ÑÇ}

/-- Complex extension of the moment generating function. -/
noncomputable
def complexMGF (X : Œ© ‚Üí ‚Ñù) (Œº : Measure Œ©) (z : ‚ÑÇ) : ‚ÑÇ := Œº[fun œâ ‚Ü¶ cexp (z * X œâ)]

lemma complexMGF_undef (hX : AEMeasurable X Œº) (h : ¬¨ Integrable (fun œâ ‚Ü¶ rexp (z.re * X œâ)) Œº) :
    complexMGF X Œº z = 0 := by
  rw [complexMGF, integral_undef]
  rw [‚Üê integrable_norm_iff (AEMeasurable.aestronglyMeasurable <| by fun_prop)]
  simpa [Complex.norm_eq_abs, Complex.abs_exp] using h

lemma complexMGF_id_map (hX : AEMeasurable X Œº) : complexMGF id (Œº.map X) = complexMGF X Œº := by
  ext t
  rw [complexMGF, integral_map hX]
  ¬∑ rfl
  ¬∑ exact AEMeasurable.aestronglyMeasurable <| by fun_prop

lemma abs_complexMGF_le_mgf : abs (complexMGF X Œº z) ‚â§ mgf X Œº z.re := by
  rw [complexMGF, ‚Üê re_add_im z]
  simp_rw [add_mul, Complex.exp_add, re_add_im, ‚Üê Complex.norm_eq_abs]
  calc ‚Äñ‚à´ œâ, cexp (z.re * X œâ) * cexp (z.im * I * X œâ) ‚àÇŒº‚Äñ
  _ ‚â§ ‚à´ œâ, ‚Äñcexp (z.re * X œâ) * cexp (z.im * I * X œâ)‚Äñ ‚àÇŒº := norm_integral_le_integral_norm _
  _ = ‚à´ œâ, rexp (z.re * X œâ) ‚àÇŒº := by simp [Complex.abs_exp]

lemma complexMGF_ofReal (x : ‚Ñù) : complexMGF X Œº x = mgf X Œº x := by
  rw [complexMGF]
  norm_cast
  have : ‚à´ œâ, (rexp (x * X œâ) : ‚ÑÇ) ‚àÇŒº = ‚à´ œâ, rexp (x * X œâ) ‚àÇŒº := integral_ofReal
  rw [this]
  simp only [mgf]

lemma re_complexMGF_ofReal (x : ‚Ñù) : (complexMGF X Œº x).re = mgf X Œº x := by
  simp [complexMGF_ofReal]

lemma re_complexMGF_ofReal' : (fun x : ‚Ñù ‚Ü¶ (complexMGF X Œº x).re) = mgf X Œº := by
  ext x
  exact re_complexMGF_ofReal x

section Analytic

lemma complexMGF_add_sub_sum (ht : t ‚â† 0)
    (h_int_pos : Integrable (fun œâ ‚Ü¶ rexp ((z.re + t) * X œâ)) Œº)
    (h_int_neg : Integrable (fun œâ ‚Ü¶ rexp ((z.re - t) * X œâ)) Œº)
    (hŒµ : |Œµ.re| ‚â§ |t|) (n : ‚Ñï) :
    complexMGF X Œº (z + Œµ) - ‚àë m in range n, Œµ ^ m / m.factorial * ‚à´ œâ, X œâ ^ m * cexp (z * X œâ) ‚àÇŒº
      = Œº[fun œâ ‚Ü¶ cexp (z * X œâ)
        * (cexp (Œµ * X œâ) - ‚àë m in range n, Œµ ^ m / m.factorial * X œâ ^ m)] := by
  have hX : AEMeasurable X Œº := aemeasurable_of_integrable_exp_mul ?_ h_int_pos h_int_neg
  swap; ¬∑ rw [‚Üê sub_ne_zero]; simp [ht]
  have hŒµ_int_pos : Integrable (fun œâ ‚Ü¶ rexp ((z.re + Œµ.re) * X œâ)) Œº := by
    refine integrable_exp_mul_of_le_of_le (a := z.re - |t|) (b := z.re + |t|) ?_ ?_ ?_ ?_
    ¬∑ rcases le_total 0 t with ht | ht
      ¬∑ rwa [_root_.abs_of_nonneg ht]
      ¬∑ simpa [abs_of_nonpos ht]
    ¬∑ rcases le_total 0 t with ht | ht
      ¬∑ rwa [_root_.abs_of_nonneg ht]
      ¬∑ rwa [abs_of_nonpos ht]
    ¬∑ rw [sub_eq_add_neg]
      gcongr
      rw [neg_le]
      exact (neg_le_abs _).trans hŒµ
    ¬∑ gcongr
      exact (le_abs_self _).trans hŒµ
  have h_int_zŒµ : Integrable (fun œâ ‚Ü¶ cexp ((z + Œµ) * X œâ)) Œº := by
    rw [‚Üê integrable_norm_iff (AEMeasurable.aestronglyMeasurable <| by fun_prop)]
    simpa only [Complex.norm_eq_abs, Complex.abs_exp, mul_re, add_re, ofReal_re, add_im, ofReal_im,
      mul_zero, sub_zero]
  have h_int_mul i : Integrable (fun œâ ‚Ü¶ X œâ ^ i * cexp (z * X œâ)) Œº := by
    rw [‚Üê integrable_norm_iff]
    swap; ¬∑ exact AEMeasurable.aestronglyMeasurable (by fun_prop)
    simp only [norm_mul, Complex.norm_eq_abs, abs_ofReal, Complex.abs_exp, mul_re, ofReal_re,
      ofReal_im, mul_zero, sub_zero]
    convert integrable_pow_abs_mul_exp_of_integrable_exp_mul ht h_int_pos h_int_neg i
    simp
  simp_rw [complexMGF, add_mul, Complex.exp_add, mul_comm _ (cexp (Œµ * X _))]
  calc ‚à´ œâ, cexp (Œµ * X œâ) * cexp (z * X œâ) ‚àÇŒº -
      ‚àë m ‚àà range n, Œµ ^ m / m.factorial * ‚à´ œâ, X œâ ^ m * cexp (z * X œâ) ‚àÇŒº
  _ = ‚à´ œâ, cexp (Œµ * X œâ) * cexp (z * X œâ) ‚àÇŒº -
      ‚àë m ‚àà range n, ‚à´ œâ, Œµ ^ m / m.factorial * X œâ ^ m * cexp (z * X œâ) ‚àÇŒº := by
    congr with m
    rw [‚Üê integral_mul_left]
    simp_rw [mul_assoc]
  _ = ‚à´ œâ, cexp (Œµ * X œâ) * cexp (z * X œâ) ‚àÇŒº -
      ‚à´ œâ, ‚àë m ‚àà range n, Œµ ^ m / m.factorial * X œâ ^ m * cexp (z * X œâ) ‚àÇŒº := by
    congr
    rw [integral_finset_sum _ fun i hi ‚Ü¶ ?_]
    simp_rw [mul_assoc]
    refine Integrable.const_mul ?_ _
    exact h_int_mul _
  _ = ‚à´ œâ, cexp (z * X œâ) * (cexp (Œµ * X œâ) - ‚àë m ‚àà range n, Œµ ^ m / m.factorial * X œâ ^ m) ‚àÇŒº := by
    rw [‚Üê integral_sub]
    ¬∑ congr with œâ
      simp_rw [mul_sub]
      congr 1
      ¬∑ rw [mul_comm]
      ¬∑ rw [mul_sum]
        congr with m
        ring
    ¬∑ simp_rw [‚Üê Complex.exp_add, ‚Üê add_mul, add_comm Œµ]
      exact h_int_zŒµ
    ¬∑ refine integrable_finset_sum _ fun m hm ‚Ü¶ ?_
      simp_rw [mul_assoc]
      refine Integrable.const_mul ?_ _
      exact h_int_mul _

lemma abs_complexMGF_add_sub_sum_le
    (h_int_pos : Integrable (fun œâ ‚Ü¶ rexp ((z.re + t) * X œâ)) Œº)
    (h_int_neg : Integrable (fun œâ ‚Ü¶ rexp ((z.re - t) * X œâ)) Œº)
    (hŒµ : abs Œµ < |t|) (n : ‚Ñï):
    abs (complexMGF X Œº (z + Œµ)
        - ‚àë m in range n, Œµ ^ m / m.factorial * ‚à´ œâ, X œâ ^ m * cexp (z * X œâ) ‚àÇŒº)
      ‚â§ (abs Œµ) ^ n * Œº[fun œâ ‚Ü¶ |X œâ| ^ n * rexp (z.re * X œâ + abs Œµ * |X œâ|)] := by
  have ht : t ‚â† 0 := by
    suffices |t| ‚â† 0 by simpa
    refine (lt_of_le_of_lt ?_ hŒµ).ne'
    exact AbsoluteValue.nonneg abs Œµ
  rw [complexMGF_add_sub_sum ht h_int_pos h_int_neg ((abs_re_le_abs Œµ).trans hŒµ.le),
    ‚Üê integral_mul_left, ‚Üê Complex.norm_eq_abs]
  refine (norm_integral_le_integral_norm _).trans ?_
  simp only [norm_mul, Complex.norm_eq_abs, Complex.abs_exp, mul_re, ofReal_re, ofReal_im, mul_zero,
    sub_zero, _root_.sq_abs]
  refine integral_mono_of_nonneg (ae_of_all _ fun œâ ‚Ü¶ ?_) ?_ (ae_of_all _ fun œâ ‚Ü¶ ?_)
  ¬∑ positivity
  ¬∑ refine Integrable.const_mul ?_ _
    exact integrable_pow_abs_mul_exp_add_of_integrable_exp_mul h_int_pos h_int_neg
      (AbsoluteValue.nonneg abs Œµ) hŒµ n
  ¬∑ simp_rw [Real.exp_add, mul_comm (rexp (z.re * X œâ)), ‚Üê mul_assoc]
    gcongr
    convert abs_exp_sub_sum_le_abs_mul_exp (Œµ * X œâ) n using 4 with m hm
    ¬∑ rw [mul_pow]
      ring
    ¬∑ simp [mul_pow]
    ¬∑ simp

lemma tendsto_integral_pow_abs_mul_exp (hz : z.re ‚àà interior (integrableExpSet X Œº)) (n : ‚Ñï) :
    Tendsto (fun h ‚Ü¶ Œº[fun œâ ‚Ü¶ |X œâ| ^ n * rexp (z.re * X œâ + abs h * |X œâ|)]) (ùìù 0)
      (ùìù Œº[fun œâ ‚Ü¶ |X œâ| ^ n * rexp (z.re * X œâ)]) := by
  have hX : AEMeasurable X Œº := aemeasurable_of_mem_interior_integrableExpSet hz
  rw [mem_interior_iff_mem_nhds, mem_nhds_iff_exists_Ioo_subset] at hz
  obtain ‚ü®l, u, hlu, h_subset‚ü© := hz
  let t := ((z.re - l) ‚äì (u - z.re)) / 2
  have h_pos : 0 < (z.re - l) ‚äì (u - z.re) := by simp [hlu.1, hlu.2]
  have ht : 0 < t := half_pos h_pos
  refine tendsto_integral_filter_of_dominated_convergence
    (fun œâ ‚Ü¶ |X œâ| ^ n * rexp (z.re * X œâ + t/2 * |X œâ|)) ?_ ?_ ?_ ?_
  ¬∑ exact .of_forall fun h ‚Ü¶ AEMeasurable.aestronglyMeasurable (by fun_prop)
  ¬∑ rw [eventually_nhds_iff]
    refine ‚ü®{x | abs x < t/2}, fun y hy ‚Ü¶ ?_, ?_, by simp [ht]‚ü©
    ¬∑ refine ae_of_all _ fun œâ ‚Ü¶ ?_
      simp only [norm_mul, norm_pow, Real.norm_eq_abs, _root_.abs_abs, Real.abs_exp]
      gcongr
      exact hy.le
    ¬∑ exact isOpen_lt Complex.continuous_abs (by fun_prop)
  ¬∑ convert integrable_pow_abs_mul_exp_add_of_integrable_exp_mul ?_ ?_ (half_pos ht).le (t := t)
      ?_ n using 3
    ¬∑ exact h_subset (add_half_inf_sub_mem_Ioo hlu)
    ¬∑ exact h_subset (sub_half_inf_sub_mem_Ioo hlu)
    ¬∑ simp [_root_.abs_of_nonneg ht.le, ht]
  ¬∑ refine ae_of_all _ fun œâ ‚Ü¶ ?_
    refine Tendsto.const_mul _ ?_
    refine (Real.continuous_exp.tendsto _).comp ?_
    nth_rw 2 [‚Üê add_zero (z.re * X œâ)]
    refine tendsto_const_nhds.add ?_
    rw [‚Üê zero_mul (|X œâ|)]
    refine Tendsto.mul_const _ ?_
    convert continuous_abs.tendsto 0
    simp

lemma isBigO_abs_complexMGF_sub_sum (hz : z.re ‚àà interior (integrableExpSet X Œº)) (n : ‚Ñï) :
    (fun Œµ ‚Ü¶ complexMGF X Œº (z + Œµ)
        - ‚àë m in range n, Œµ ^ m / m.factorial * ‚à´ œâ, X œâ ^ m * cexp (z * X œâ) ‚àÇŒº)
      =O[ùìù 0] fun Œµ ‚Ü¶ (abs Œµ) ^ n := by
  have hz' := hz
  rw [mem_interior_iff_mem_nhds, mem_nhds_iff_exists_Ioo_subset] at hz'
  obtain ‚ü®l, u, hlu, h_subset‚ü© := hz'
  let t := ((z.re - l) ‚äì (u - z.re)) / 2
  have h_pos : 0 < (z.re - l) ‚äì (u - z.re) := by simp [hlu.1, hlu.2]
  have ht : 0 < t := half_pos h_pos
  calc
  _ =O[ùìù 0] fun Œµ : ‚ÑÇ ‚Ü¶ (abs Œµ) ^ n * Œº[fun œâ ‚Ü¶ |X œâ| ^ n * rexp (z.re * X œâ + abs Œµ * |X œâ|)] := by
    refine Eventually.isBigO ?_
    rw [eventually_nhds_iff]
    refine ‚ü®{x | abs x < t}, fun y hy ‚Ü¶ ?_, ?_, by simp [ht]‚ü©
    ¬∑ simp only [Real.norm_eq_abs, Complex.abs_abs]
      refine abs_complexMGF_add_sub_sum_le ?_ ?_ (hy.trans_le (le_abs_self _)) n
      ¬∑ exact h_subset (add_half_inf_sub_mem_Ioo hlu)
      ¬∑ exact h_subset (sub_half_inf_sub_mem_Ioo hlu)
    ¬∑ exact isOpen_lt (by fun_prop) (by fun_prop)
  _ =O[ùìù 0] fun Œµ ‚Ü¶ (abs Œµ) ^ n * 1 := by
    refine Asymptotics.IsBigO.mul (Asymptotics.isBigO_refl _ _) ?_
    refine Tendsto.isBigO_one _ (c := Œº[fun œâ ‚Ü¶ |X œâ| ^ n * rexp (z.re * X œâ)]) ?_
    exact tendsto_integral_pow_abs_mul_exp hz n
  _ = fun Œµ ‚Ü¶ (abs Œµ) ^ n := by simp

/-- For all `z : ‚ÑÇ` such that the real part `z.re` belongs to the interior
  of the interval of definition of the mgf, `complexMGF X Œº` is differentiable at `z`
  with derivative `Œº[X * exp (z * X)]`. -/
theorem hasDerivAt_complexMGF (hz : z.re ‚àà interior (integrableExpSet X Œº)) :
    HasDerivAt (complexMGF X Œº) Œº[fun œâ ‚Ü¶ X œâ * cexp (z * X œâ)] z := by
  rw [hasDerivAt_iff_isLittleO_nhds_zero]
  simp only [smul_eq_mul]
  calc (fun h ‚Ü¶ complexMGF X Œº (z + h) - complexMGF X Œº z - h * ‚à´ œâ, X œâ * cexp (z * X œâ) ‚àÇŒº)
  _ =O[ùìù 0] fun h ‚Ü¶ (abs h)^2 := by
    convert isBigO_abs_complexMGF_sub_sum hz 2 using 2
    simp [sum_range, sub_add_eq_sub_sub, complexMGF]
  _ =o[ùìù 0] fun h ‚Ü¶ h := Asymptotics.isLittleO_norm_pow_id one_lt_two

/-- `complexMGF X Œº` is holomorphic on the vertical strip
`{z | z.re ‚àà interior (integrableExpSet X Œº)}`. -/
theorem differentiableOn_complexMGF :
    DifferentiableOn ‚ÑÇ (complexMGF X Œº) {z | z.re ‚àà interior (integrableExpSet X Œº)} := by
  intro z hz
  have h := hasDerivAt_complexMGF hz
  rw [hasDerivAt_iff_hasFDerivAt] at h
  exact h.hasFDerivWithinAt.differentiableWithinAt

/-- `complexMGF X Œº` is analytic on the vertical strip
  `{z | z.re ‚àà interior (integrableExpSet X Œº)}`. -/
theorem analyticOnNhd_complexMGF :
    AnalyticOnNhd ‚ÑÇ (complexMGF X Œº) {z | z.re ‚àà interior (integrableExpSet X Œº)} :=
  differentiableOn_complexMGF.analyticOnNhd (isOpen_interior.preimage Complex.continuous_re)

/-- `complexMGF X Œº` is analytic at any point `z` with `z.re ‚àà interior (integrableExpSet X Œº)`. -/
lemma analyticAt_complexMGF (hz : z.re ‚àà interior (integrableExpSet X Œº)) :
    AnalyticAt ‚ÑÇ (complexMGF X Œº) z := analyticOnNhd_complexMGF z hz

end Analytic

section Deriv

/-! ### Derivatives of `complexMGF` -/

/-- For `z : ‚ÑÇ` with `z.re ‚àà interior (integrableExpSet X Œº)`, the derivative of the function
`z' ‚Ü¶ Œº[X ^ n * cexp (z' * X)]` at `z` is `Œº[X ^ (n + 1) * cexp (z * X)]`. -/
lemma hasDerivAt_integral_pow_mul_exp (hz : z.re ‚àà interior (integrableExpSet X Œº)) (n : ‚Ñï) :
    HasDerivAt (fun z ‚Ü¶ Œº[fun œâ ‚Ü¶ X œâ ^ n * cexp (z * X œâ)])
        Œº[fun œâ ‚Ü¶ X œâ ^ (n + 1) * cexp (z * X œâ)] z := by
  have hX : AEMeasurable X Œº := aemeasurable_of_mem_interior_integrableExpSet hz
  have hz' := hz
  rw [mem_interior_iff_mem_nhds, mem_nhds_iff_exists_Ioo_subset] at hz'
  obtain ‚ü®l, u, hlu, h_subset‚ü© := hz'
  let t := ((z.re - l) ‚äì (u - z.re)) / 2
  have h_pos : 0 < (z.re - l) ‚äì (u - z.re) := by simp [hlu.1, hlu.2]
  have ht : 0 < t := half_pos h_pos
  refine (hasDerivAt_integral_of_dominated_loc_of_deriv_le
    (bound := fun œâ ‚Ü¶ |X œâ| ^ (n + 1) * rexp (z.re * X œâ + t/2 * |X œâ|))
    (F := fun z œâ ‚Ü¶ X œâ ^ n * cexp (z * X œâ))
    (F' := fun z œâ ‚Ü¶ X œâ ^ (n + 1) * cexp (z * X œâ)) (half_pos ht) ?_ ?_ ?_ ?_ ?_ ?_).2
  ¬∑ exact .of_forall fun z ‚Ü¶ AEMeasurable.aestronglyMeasurable (by fun_prop)
  ¬∑ rw [‚Üê integrable_norm_iff]
    swap; ¬∑ exact AEMeasurable.aestronglyMeasurable (by fun_prop)
    simp only [norm_mul, Complex.norm_eq_abs, abs_ofReal, Complex.abs_exp, mul_re, ofReal_re,
      ofReal_im, mul_zero, sub_zero]
    convert integrable_pow_abs_mul_exp_of_mem_interior_integrableExpSet hz n
    simp
  ¬∑ exact AEMeasurable.aestronglyMeasurable (by fun_prop)
  ¬∑ refine ae_of_all _ fun œâ Œµ hŒµ ‚Ü¶ ?_
    simp only [norm_mul, norm_pow, norm_real, Real.norm_eq_abs, Complex.norm_eq_abs]
    rw [Complex.abs_ofReal, Complex.abs_exp]
    simp only [mul_re, ofReal_re, ofReal_im, mul_zero, sub_zero]
    gcongr
    have : Œµ = z + (Œµ - z) := by simp
    rw [this, add_re, add_mul]
    gcongr _ + ?_
    refine (le_abs_self _).trans ?_
    rw [abs_mul]
    gcongr
    refine (abs_re_le_abs _).trans ?_
    simp only [Metric.mem_ball, dist_eq_norm, Complex.norm_eq_abs] at hŒµ
    exact hŒµ.le
  ¬∑ refine integrable_pow_abs_mul_exp_add_of_integrable_exp_mul ?_ ?_ ?_ ?_ (t := t) (n + 1)
    ¬∑ exact h_subset (add_half_inf_sub_mem_Ioo hlu)
    ¬∑ exact h_subset (sub_half_inf_sub_mem_Ioo hlu)
    ¬∑ positivity
    ¬∑ refine lt_of_lt_of_le ?_ (le_abs_self _)
      simp [ht]
  ¬∑ refine ae_of_all _ fun œâ Œµ hŒµ ‚Ü¶ ?_
    simp only
    simp_rw [pow_succ, mul_assoc]
    refine HasDerivAt.const_mul _ ?_
    simp_rw [‚Üê smul_eq_mul, Complex.exp_eq_exp_‚ÑÇ]
    convert hasDerivAt_exp_smul_const (X œâ : ‚ÑÇ) Œµ using 1
    rw [smul_eq_mul, mul_comm]

/-- For `t : ‚Ñù` with `t ‚àà interior (integrableExpSet X Œº)`, the derivative of the function
`x ‚Ü¶ Œº[X ^ n * rexp (x * X)]` at `t` is `Œº[X ^ (n + 1) * rexp (t * X)]`. -/
lemma hasDerivAt_integral_pow_mul_exp_real (ht : t ‚àà interior (integrableExpSet X Œº)) (n : ‚Ñï) :
    HasDerivAt (fun t ‚Ü¶ Œº[fun œâ ‚Ü¶ X œâ ^ n * rexp (t * X œâ)])
      Œº[fun œâ ‚Ü¶ X œâ ^ (n + 1) * rexp (t * X œâ)] t := by
  have hX : AEMeasurable X Œº := aemeasurable_of_mem_interior_integrableExpSet ht
  have h_re_of_mem n t (ht' : t ‚àà interior (integrableExpSet X Œº)) :
      (‚à´ œâ, X œâ ^ n * cexp (t * X œâ) ‚àÇŒº).re = ‚à´ œâ, X œâ ^ n * rexp (t * X œâ) ‚àÇŒº := by
    rw [‚Üê RCLike.re_eq_complex_re, ‚Üê integral_re]
    ¬∑ norm_cast
    ¬∑ rw [‚Üê integrable_norm_iff]
      swap; ¬∑ exact AEMeasurable.aestronglyMeasurable (by fun_prop)
      simp only [norm_mul, Complex.norm_eq_abs, abs_ofReal, Complex.abs_exp, mul_re, ofReal_re,
        ofReal_im, mul_zero, sub_zero, Complex.abs_pow]
      exact integrable_pow_abs_mul_exp_of_mem_interior_integrableExpSet ht' n
  have h_re n : ‚àÄ·∂† t' : ‚Ñù in ùìù t, (‚à´ œâ, X œâ ^ n * cexp (t' * X œâ) ‚àÇŒº).re
      = ‚à´ œâ, X œâ ^ n * rexp (t' * X œâ) ‚àÇŒº := by
    filter_upwards [isOpen_interior.eventually_mem ht] with t ht' using h_re_of_mem n t ht'
  rw [‚Üê EventuallyEq.hasDerivAt_iff (h_re _), ‚Üê h_re_of_mem _ t ht]
  exact (hasDerivAt_integral_pow_mul_exp (by simp [ht]) n).real_of_complex

lemma hasDerivAt_iteratedDeriv_complexMGF (hz : z.re ‚àà interior (integrableExpSet X Œº)) (n : ‚Ñï) :
    HasDerivAt (iteratedDeriv n (complexMGF X Œº)) Œº[fun œâ ‚Ü¶ X œâ ^ (n + 1) * cexp (z * X œâ)] z := by
  induction n generalizing z with
  | zero => simp [hasDerivAt_complexMGF hz]
  | succ n hn =>
    rw [iteratedDeriv_succ]
    have : deriv (iteratedDeriv n (complexMGF X Œº))
        =·∂†[ùìù z] fun z ‚Ü¶ Œº[fun œâ ‚Ü¶ X œâ ^ (n + 1) * cexp (z * X œâ)] := by
      have h_mem : ‚àÄ·∂† y in ùìù z, y.re ‚àà interior (integrableExpSet X Œº) := by
        refine IsOpen.eventually_mem ?_ hz
        exact isOpen_interior.preimage Complex.continuous_re
      filter_upwards [h_mem] with y hy using HasDerivAt.deriv (hn hy)
    rw [EventuallyEq.hasDerivAt_iff this]
    exact hasDerivAt_integral_pow_mul_exp hz (n + 1)

/-- For `z : ‚ÑÇ` with `z.re ‚àà interior (integrableExpSet X Œº)`, the n-th derivative of the function
`complexMGF X Œº` at `z` is `Œº[X ^ n * cexp (z * X)]`. -/
lemma iteratedDeriv_complexMGF (hz : z.re ‚àà interior (integrableExpSet X Œº)) (n : ‚Ñï) :
    iteratedDeriv n (complexMGF X Œº) z = Œº[fun œâ ‚Ü¶ X œâ ^ n * cexp (z * X œâ)] := by
  induction n generalizing z with
  | zero => simp [complexMGF]
  | succ n hn =>
    rw [iteratedDeriv_succ]
    exact (hasDerivAt_iteratedDeriv_complexMGF hz n).deriv

end Deriv

section EqOfMGF

/-! We prove that if two random variables have the same `mgf`, then
they also have the same `complexMGF`.-/

variable {Œ©' : Type*} {mŒ©' : MeasurableSpace Œ©'} {Y : Œ©' ‚Üí ‚Ñù} {Œº' : Measure Œ©'}

/-- If two random variables have the same moment generating function then they have
the same `integrableExpSet`. -/
lemma integrableExpSet_eq_of_mgf' (hXY : mgf X Œº = mgf Y Œº') (hŒºŒº' : Œº = 0 ‚Üî Œº' = 0) :
    integrableExpSet X Œº = integrableExpSet Y Œº' := by
  ext t
  simp only [integrableExpSet, Set.mem_setOf_eq]
  by_cases hŒº : Œº = 0
  ¬∑ simp [hŒº, hŒºŒº'.mp hŒº]
  have hŒº' : Œº' ‚â† 0 := (not_iff_not.mpr hŒºŒº').mp hŒº
  rw [‚Üê mgf_pos_iff' hŒº, ‚Üê mgf_pos_iff' hŒº', hXY]

/-- If two random variables have the same moment generating function then they have
the `integrableExpSet`. -/
lemma integrableExpSet_eq_of_mgf [IsProbabilityMeasure Œº] [IsProbabilityMeasure Œº']
    (hXY : mgf X Œº = mgf Y Œº') :
    integrableExpSet X Œº = integrableExpSet Y Œº' :=
  integrableExpSet_eq_of_mgf' hXY <| by simp [IsProbabilityMeasure.ne_zero]

/-- If two random variables have the same moment generating function then they have
the same `complexMGF` on the vertical strip `{z | z.re ‚àà interior (integrableExpSet X Œº)}`. -/
-- todo: the equality also holds on `(integrableExpSet X Œº)·∂ú` since both are zero there. What about
-- the two (at most) vertical lines corresponding to the extemities of the interval?
lemma eqOn_complexMGF_of_mgf' (hXY : mgf X Œº = mgf Y Œº') (hŒºŒº' : Œº = 0 ‚Üî Œº' = 0) :
    Set.EqOn (complexMGF X Œº) (complexMGF Y Œº') {z | z.re ‚àà interior (integrableExpSet X Œº)} := by
  by_cases h_empty : interior (integrableExpSet X Œº) = ‚àÖ
  ¬∑ simp [h_empty]
  rw [‚Üê ne_eq, ‚Üê Set.nonempty_iff_ne_empty] at h_empty
  obtain ‚ü®t, ht‚ü© := h_empty
  have hX : AnalyticOnNhd ‚ÑÇ (complexMGF X Œº) {z | z.re ‚àà interior (integrableExpSet X Œº)} :=
    analyticOnNhd_complexMGF
  have hY : AnalyticOnNhd ‚ÑÇ (complexMGF Y Œº') {z | z.re ‚àà interior (integrableExpSet Y Œº')} :=
    analyticOnNhd_complexMGF
  rw [integrableExpSet_eq_of_mgf' hXY hŒºŒº'] at hX ht ‚ä¢
  refine AnalyticOnNhd.eqOn_of_preconnected_of_frequently_eq hX hY
    (convex_integrableExpSet.interior.linear_preimage reLm).isPreconnected
    (z‚ÇÄ := (t : ‚ÑÇ)) (by simp [ht]) ?_
  have h_real : ‚àÉ·∂† (x : ‚Ñù) in ùìù[‚â†] t, complexMGF X Œº x = complexMGF Y Œº' x := by
    refine .of_forall fun y ‚Ü¶ ?_
    rw [complexMGF_ofReal, complexMGF_ofReal, hXY]
  rw [frequently_iff_seq_forall] at h_real ‚ä¢
  obtain ‚ü®xs, hx_tendsto, hx_eq‚ü© := h_real
  refine ‚ü®fun n ‚Ü¶ xs n, ?_, fun n ‚Ü¶ ?_‚ü©
  ¬∑ rw [tendsto_nhdsWithin_iff] at hx_tendsto ‚ä¢
    constructor
    ¬∑ rw [tendsto_ofReal_iff]
      exact hx_tendsto.1
    ¬∑ simpa using hx_tendsto.2
  ¬∑ simp [hx_eq]

/-- If two random variables have the same moment generating function then they have
the same `complexMGF` on the vertical strip `{z | z.re ‚àà interior (integrableExpSet X Œº)}`. -/
lemma eqOn_complexMGF_of_mgf [IsProbabilityMeasure Œº] [IsProbabilityMeasure Œº']
    (hXY : mgf X Œº = mgf Y Œº') :
    Set.EqOn (complexMGF X Œº) (complexMGF Y Œº') {z | z.re ‚àà interior (integrableExpSet X Œº)} :=
  eqOn_complexMGF_of_mgf' hXY <| by simp [IsProbabilityMeasure.ne_zero]

end EqOfMGF

end ProbabilityTheory
