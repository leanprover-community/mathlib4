/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Analysis.LocallyConvex.ContinuousOfBounded
import Mathlib.MeasureTheory.Constructions.BorelSpace.ContinuousLinearMap
import Mathlib.MeasureTheory.Function.L2Space

/-!
# Covariance in Banach spaces

We define the covariance of a measure in a Banach space, as a continous bilinear form.

## Main definitions

Let `Î¼` be a finite measure on a Banach space `E` with the Borel Ïƒ-algebra, such that
`âˆ« x, â€–xâ€–^2 âˆ‚Î¼ < âˆž`.

* `covarianceBilin` : covariance of the measure `Î¼` as a continuous bilinear form
  `(E â†’L[â„] â„) â†’L[â„] (E â†’L[â„] â„) â†’L[â„] â„`

## Main statements

* `covarianceBilin_apply` : the covariance of `Î¼` on `Lâ‚, Lâ‚‚ : E â†’L[â„] â„` is equal to
  `âˆ« x, (Lâ‚ x - Î¼[Lâ‚]) * (Lâ‚‚ x - Î¼[Lâ‚‚]) âˆ‚Î¼`.

-/


open MeasureTheory ProbabilityTheory Complex NormedSpace
open scoped ENNReal NNReal Real Topology

section Aux

variable {Î± F : Type*} {mÎ± : MeasurableSpace Î±} {Î¼ : Measure Î±} [NormedAddCommGroup F] {f : Î± â†’ F}

lemma rpow_toReal_eLpNorm {p : â„} (hf : MemLp f (ENNReal.ofReal p) Î¼) (hp : 0 < p) :
    (eLpNorm f (ENNReal.ofReal p) Î¼).toReal ^ p = âˆ« x, â€–f xâ€– ^ p âˆ‚Î¼ := by
  rw [eLpNorm_eq_lintegral_rpow_enorm (by simp [hp]) (by simp)]
  simp only [one_div]
  have : (ENNReal.ofReal p).toReal = p := ENNReal.toReal_ofReal (by positivity)
  simp_rw [this]
  rw [ENNReal.toReal_rpow, â† ENNReal.rpow_mul, inv_mul_cancelâ‚€ hp.ne', ENNReal.rpow_one]
  simp_rw [â† ofReal_norm, ENNReal.ofReal_rpow_of_nonneg (norm_nonneg _) hp.le]
  rw [â† ofReal_integral_eq_lintegral_ofReal, ENNReal.toReal_ofReal (by positivity)]
  Â· convert MemLp.integrable_norm_rpow hf (by simp [hp]) (by simp)
    exact this.symm
  Â· exact ae_of_all _ fun x â†¦ by positivity

lemma pow_toReal_eLpNorm {n : â„•} (hf : MemLp f n Î¼) (hn : n â‰  0) :
    (eLpNorm f n Î¼).toReal ^ n = âˆ« x, â€–f xâ€– ^ n âˆ‚Î¼ := by
  have h_Lp : MemLp f (ENNReal.ofReal n) Î¼ := by convert hf; simp
  have h := rpow_toReal_eLpNorm h_Lp (by positivity)
  simpa using h

end Aux

namespace ProbabilityTheory

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
  {mE : MeasurableSpace E} {Î¼ : Measure E}

section ToLp

variable [OpensMeasurableSpace E] {p : â„â‰¥0âˆž}

lemma _root_.MeasureTheory.MemLp.continuousLinearMap (h_Lp : MemLp id p Î¼) (L : E â†’L[â„] â„) :
    MemLp L p Î¼ := by
  refine MemLp.mono (g := fun x â†¦ â€–Lâ€– â€¢ x) (h_Lp.const_smul _) ?_ ?_
  Â· exact Measurable.aestronglyMeasurable <| by fun_prop
  refine ae_of_all _ fun x â†¦ ?_
  simp only [norm_smul, norm_norm]
  exact L.le_opNorm x

lemma _root_.MeasureTheory.MemLp.integrable_continuousLinearMap
    (h_Lp : MemLp id 1 Î¼) (L : E â†’L[â„] â„) :
    Integrable L Î¼ := by
  rw [â† memLp_one_iff_integrable]
  exact h_Lp.continuousLinearMap L

/-- `MemLp.toLp` as a `LinearMap` from the continuous linear maps. -/
def ContinuousLinearMap.toLpâ‚— (Î¼ : Measure E) (p : â„â‰¥0âˆž) (h_Lp : MemLp id p Î¼) :
    (E â†’L[â„] â„) â†’â‚—[â„] Lp â„ p Î¼ where
  toFun := fun L â†¦ MemLp.toLp L (h_Lp.continuousLinearMap L)
  map_add' u v := by push_cast; rw [MemLp.toLp_add]
  map_smul' c L := by push_cast; rw [MemLp.toLp_const_smul]; rfl

@[simp]
lemma ContinuousLinearMap.toLpâ‚—_apply (h_Lp : MemLp id p Î¼) (L : E â†’L[â„] â„) :
    L.toLpâ‚— Î¼ p h_Lp = MemLp.toLp L (h_Lp.continuousLinearMap L) := rfl

lemma norm_toLpâ‚—_le (h_Lp : MemLp id p Î¼) (L : E â†’L[â„] â„) (hp : p â‰  0) (hp_top : p â‰  âˆž) :
    â€–L.toLpâ‚— Î¼ p h_Lpâ€– â‰¤ â€–Lâ€– * (eLpNorm id p Î¼).toReal := by
  have h0 : 0 < p.toReal := by simp [ENNReal.toReal_pos_iff, pos_iff_ne_zero, hp, hp_top.lt_top]
  suffices â€–L.toLpâ‚— Î¼ p h_Lpâ€–
      â‰¤ (â€–Lâ€–â‚‘ ^ p.toReal * âˆ«â» x, â€–xâ€–â‚‘ ^ p.toReal âˆ‚Î¼).toReal ^ p.toRealâ»Â¹ by
    refine this.trans_eq ?_
    simp only [ENNReal.toReal_mul]
    rw [â† ENNReal.toReal_rpow, Real.mul_rpow (by positivity) (by positivity),
      â† Real.rpow_mul (by positivity), mul_inv_cancelâ‚€ h0.ne', Real.rpow_one, toReal_enorm]
    rw [eLpNorm_eq_lintegral_rpow_enorm (by simp [hp]) hp_top, ENNReal.toReal_rpow]
    simp
  rw [ContinuousLinearMap.toLpâ‚—_apply, Lp.norm_toLp,
    eLpNorm_eq_lintegral_rpow_enorm (by simp [hp]) hp_top]
  simp only [ENNReal.toReal_ofNat, ENNReal.rpow_ofNat, one_div]
  refine ENNReal.toReal_le_of_le_ofReal (by positivity) ?_
  suffices âˆ«â» x, â€–L xâ€–â‚‘ ^ p.toReal âˆ‚Î¼ â‰¤ â€–Lâ€–â‚‘ ^ p.toReal * âˆ«â» x, â€–xâ€–â‚‘ ^ p.toReal âˆ‚Î¼ by
    rw [â† ENNReal.ofReal_rpow_of_nonneg (by positivity) (by positivity)]
    gcongr
    rwa [ENNReal.ofReal_toReal]
    refine ENNReal.mul_ne_top (by simp) ?_
    have h := h_Lp.eLpNorm_ne_top
    rw [eLpNorm_eq_lintegral_rpow_enorm (by simp [hp]) hp_top] at h
    simpa [h0] using h
  calc âˆ«â» x, â€–L xâ€–â‚‘ ^ p.toReal âˆ‚Î¼
  _ â‰¤ âˆ«â» x, â€–Lâ€–â‚‘ ^ p.toReal * â€–xâ€–â‚‘ ^ p.toReal âˆ‚Î¼ := by
    refine lintegral_mono fun x â†¦ ?_
    rw [â† ENNReal.mul_rpow_of_nonneg]
    swap; Â· positivity
    gcongr
    simp_rw [â† ofReal_norm]
    rw [â† ENNReal.ofReal_mul (by positivity)]
    gcongr
    exact L.le_opNorm x
  _ = â€–Lâ€–â‚‘ ^ p.toReal * âˆ«â» x, â€–xâ€–â‚‘ ^ p.toReal âˆ‚Î¼ := by rw [lintegral_const_mul]; fun_prop

/-- `MemLp.toLp` as a `ContinuousLinearMap` from the continuous linear forms. -/
def ContinuousLinearMap.toLp (Î¼ : Measure E) (p : â„â‰¥0âˆž) [Fact (1 â‰¤ p)] (h_Lp : MemLp id p Î¼)
    (hp : p â‰  âˆž) :
    (E â†’L[â„] â„) â†’L[â„] Lp â„ p Î¼ where
  toLinearMap := ContinuousLinearMap.toLpâ‚— Î¼ p h_Lp
  cont := by
    refine LinearMap.continuous_of_locally_bounded _ fun s hs â†¦ ?_
    rw [image_isVonNBounded_iff]
    simp_rw [isVonNBounded_iff'] at hs
    obtain âŸ¨r, hxrâŸ© := hs
    refine âŸ¨r * (eLpNorm id p Î¼).toReal, fun L hLs â†¦ ?_âŸ©
    specialize hxr L hLs
    have hp_ne : p â‰  0 := by
      have : 1 â‰¤ p := Fact.out
      positivity
    refine (norm_toLpâ‚—_le h_Lp L hp_ne hp).trans ?_
    gcongr

@[simp]
lemma ContinuousLinearMap.toLp_apply (L : E â†’L[â„] â„)
    [Fact (1 â‰¤ p)] (h_Lp : MemLp id p Î¼) (hp : p â‰  âˆž) :
    L.toLp Î¼ p h_Lp hp = MemLp.toLp L (h_Lp.continuousLinearMap L) := rfl

end ToLp

section Covariance

section Centered

variable [OpensMeasurableSpace E]

/-- Continuous bilinear form with value `âˆ« x, Lâ‚ x * Lâ‚‚ x âˆ‚Î¼` on `(Lâ‚, Lâ‚‚)`.
This is the covariance only if `Î¼` is centered. -/
noncomputable
def centeredCovariance (Î¼ : Measure E) (h : MemLp id 2 Î¼) :
    (E â†’L[â„] â„) â†’L[â„] (E â†’L[â„] â„) â†’L[â„] â„ :=
  ContinuousLinearMap.bilinearComp (isBoundedBilinearMap_inner (ð•œ := â„)).toContinuousLinearMap
    (ContinuousLinearMap.toLp Î¼ 2 h (by simp)) (ContinuousLinearMap.toLp Î¼ 2 h (by simp))

lemma centeredCovariance_apply (h : MemLp id 2 Î¼) (Lâ‚ Lâ‚‚ : E â†’L[â„] â„) :
    centeredCovariance Î¼ h Lâ‚ Lâ‚‚ = âˆ« x, Lâ‚ x * Lâ‚‚ x âˆ‚Î¼ := by
  simp only [centeredCovariance, ContinuousLinearMap.bilinearComp_apply,
    ContinuousLinearMap.toLp_apply, L2.inner_def,
    RCLike.inner_apply, conj_trivial]
  refine integral_congr_ae ?_
  filter_upwards [MemLp.coeFn_toLp (h.continuousLinearMap Lâ‚),
    MemLp.coeFn_toLp (h.continuousLinearMap Lâ‚‚)] with x hxLâ‚ hxLâ‚‚
  rw [hxLâ‚, hxLâ‚‚, mul_comm]

lemma norm_centeredCovariance_le (h : MemLp id 2 Î¼) (Lâ‚ Lâ‚‚ : E â†’L[â„] â„) :
    â€–centeredCovariance Î¼ h Lâ‚ Lâ‚‚â€– â‰¤ â€–Lâ‚â€– * â€–Lâ‚‚â€– * âˆ« x, â€–xâ€– ^ 2 âˆ‚Î¼ := by
  calc â€–centeredCovariance Î¼ h Lâ‚ Lâ‚‚â€–
  _ = â€–âˆ« x, Lâ‚ x * Lâ‚‚ x âˆ‚Î¼â€– := by rw [centeredCovariance_apply]
  _ â‰¤ âˆ« x, â€–Lâ‚ xâ€– * â€–Lâ‚‚ xâ€– âˆ‚Î¼ := (norm_integral_le_integral_norm _).trans (by simp)
  _ â‰¤ âˆ« x, â€–Lâ‚â€– * â€–xâ€– * â€–Lâ‚‚â€– * â€–xâ€– âˆ‚Î¼ := by
    refine integral_mono_ae ?_ ?_ (ae_of_all _ fun x â†¦ ?_)
    Â· simp_rw [â† norm_mul]
      exact (MemLp.integrable_mul (h.continuousLinearMap Lâ‚) (h.continuousLinearMap Lâ‚‚)).norm
    Â· simp_rw [mul_assoc]
      refine Integrable.const_mul ?_ _
      simp_rw [â† mul_assoc, mul_comm _ (â€–Lâ‚‚â€–), mul_assoc, â† pow_two]
      refine Integrable.const_mul ?_ _
      exact h.integrable_norm_pow (by simp)
    Â· simp only
      rw [mul_assoc]
      gcongr
      Â· exact ContinuousLinearMap.le_opNorm Lâ‚ x
      Â· exact ContinuousLinearMap.le_opNorm Lâ‚‚ x
  _ = â€–Lâ‚â€– * â€–Lâ‚‚â€– * âˆ« x, â€–xâ€– ^ 2 âˆ‚Î¼ := by
    rw [â† integral_const_mul]
    congr with x
    ring

lemma norm_centeredCovariance_le' (h : MemLp id 2 Î¼) (Lâ‚ Lâ‚‚ : E â†’L[â„] â„) :
    â€–centeredCovariance Î¼ h Lâ‚ Lâ‚‚â€– â‰¤ â€–Lâ‚â€– * â€–Lâ‚‚â€– * (eLpNorm id 2 Î¼).toReal ^ 2 := by
  calc â€–centeredCovariance Î¼ h Lâ‚ Lâ‚‚â€–
  _ â‰¤ â€–Lâ‚â€– * â€–Lâ‚‚â€– * âˆ« x, â€–xâ€– ^ 2 âˆ‚Î¼ := norm_centeredCovariance_le _ _ _
  _ = â€–Lâ‚â€– * â€–Lâ‚‚â€– * (eLpNorm id 2 Î¼).toReal ^ 2 := by
    congr
    have h := pow_toReal_eLpNorm h (by simp)
    simpa only [ENNReal.ofReal_ofNat, Real.rpow_two, id_eq] using h.symm

end Centered

variable [BorelSpace E] [SecondCountableTopology E] [CompleteSpace E]

/-- Continuous bilinear form with value `âˆ« x, (Lâ‚ x - Î¼[Lâ‚]) * (Lâ‚‚ x - Î¼[Lâ‚‚]) âˆ‚Î¼` on `(Lâ‚, Lâ‚‚)`. -/
noncomputable
def covarianceBilin (Î¼ : Measure E) [IsFiniteMeasure Î¼] (h : MemLp id 2 Î¼) :
    (E â†’L[â„] â„) â†’L[â„] (E â†’L[â„] â„) â†’L[â„] â„ :=
  centeredCovariance (Î¼.map (fun x â†¦ x - âˆ« x, x âˆ‚Î¼)) <| by
    rw [memLp_map_measure_iff]
    Â· exact h.sub (memLp_const _)
    Â· exact Measurable.aestronglyMeasurable <| by fun_prop
    Â· fun_prop

lemma covarianceBilin_apply [IsFiniteMeasure Î¼] (h : MemLp id 2 Î¼) (Lâ‚ Lâ‚‚ : E â†’L[â„] â„) :
    covarianceBilin Î¼ h Lâ‚ Lâ‚‚ = âˆ« x, (Lâ‚ x - âˆ« x, Lâ‚ x âˆ‚Î¼) * (Lâ‚‚ x - âˆ« x, Lâ‚‚ x âˆ‚Î¼) âˆ‚Î¼ := by
  rw [covarianceBilin, centeredCovariance_apply, integral_map]
  Â· simp [â† ContinuousLinearMap.integral_comm_of_memLp_id (h.mono_exponent (by simp))]
  Â· fun_prop
  Â· exact Measurable.aestronglyMeasurable <| by fun_prop

end Covariance

end ProbabilityTheory
