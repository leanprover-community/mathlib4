/-
Copyright (c) 2025 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import Mathlib.Probability.Distributions.Gaussian
import Mathlib.Analysis.SpecialFunctions.Gaussian.FourierTransform
import Mathlib.MeasureTheory.Measure.CharacteristicFunction
import Mathlib.Analysis.LocallyConvex.ContinuousOfBounded

/-!
# Covariance in Banach spaces

We define the covariance of a measure in a Banach space, as a continous bilinear form.

## Main definitions

* `FooBar`

## Main statements

* `fooBar_unique`

## Notation



## Implementation details



## References

* [F. Bar, *Quuxes*][bibkey]

-/


open MeasureTheory ProbabilityTheory Complex NormedSpace
open scoped ENNReal NNReal Real Topology

section Aux

variable {Œ± F : Type*} {mŒ± : MeasurableSpace Œ±} {Œº : Measure Œ±} [NormedAddCommGroup F] {f : Œ± ‚Üí F}

lemma rpow_toReal_eLpNorm {p : ‚Ñù} (hf : MemLp f (ENNReal.ofReal p) Œº) (hp : 0 < p) :
    (eLpNorm f (ENNReal.ofReal p) Œº).toReal ^ p = ‚à´ x, ‚Äñf x‚Äñ ^ p ‚àÇŒº := by
  rw [eLpNorm_eq_lintegral_rpow_enorm (by simp [hp]) (by simp)]
  simp only [one_div]
  have : (ENNReal.ofReal p).toReal = p := ENNReal.toReal_ofReal (by positivity)
  simp_rw [this]
  rw [ENNReal.toReal_rpow, ‚Üê ENNReal.rpow_mul, inv_mul_cancel‚ÇÄ hp.ne', ENNReal.rpow_one]
  simp_rw [‚Üê ofReal_norm, ENNReal.ofReal_rpow_of_nonneg (norm_nonneg _) hp.le]
  rw [‚Üê ofReal_integral_eq_lintegral_ofReal, ENNReal.toReal_ofReal (by positivity)]
  ¬∑ convert MemLp.integrable_norm_rpow hf (by simp [hp]) (by simp)
    exact this.symm
  ¬∑ exact ae_of_all _ fun x ‚Ü¶ by positivity

lemma pow_toReal_eLpNorm {n : ‚Ñï} (hf : MemLp f n Œº) (hn : n ‚â† 0) :
    (eLpNorm f n Œº).toReal ^ n = ‚à´ x, ‚Äñf x‚Äñ ^ n ‚àÇŒº := by
  have h_Lp : MemLp f (ENNReal.ofReal n) Œº := by convert hf; simp
  have h := rpow_toReal_eLpNorm h_Lp (by positivity)
  simpa using h

end Aux

namespace ProbabilityTheory

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
  {mE : MeasurableSpace E}  {Œº : Measure E}

section ToLp

variable [OpensMeasurableSpace E] {p : ‚Ñù‚â•0‚àû}

lemma _root_.MeasureTheory.MemLp.continuousLinearMap (h_Lp : MemLp id p Œº) (L : E ‚ÜíL[‚Ñù] ‚Ñù) :
    MemLp L p Œº := by
  refine MemLp.mono (g := fun x ‚Ü¶ ‚ÄñL‚Äñ ‚Ä¢ x) (h_Lp.const_smul _) ?_ ?_
  ¬∑ exact Measurable.aestronglyMeasurable <| by fun_prop
  refine ae_of_all _ fun x ‚Ü¶ ?_
  simp only [norm_smul, norm_norm]
  exact L.le_opNorm x

lemma _root_.MeasureTheory.MemLp.integrable_continuousLinearMap
    (h_Lp : MemLp id 1 Œº) (L : E ‚ÜíL[‚Ñù] ‚Ñù) :
    Integrable L Œº := by
  rw [‚Üê memLp_one_iff_integrable]
  exact h_Lp.continuousLinearMap L

/-- `MemLp.toLp` as a `LinearMap` from the continuous linear maps. -/
def ContinuousLinearMap.toLp‚Çó (Œº : Measure E) (p : ‚Ñù‚â•0‚àû) (h_Lp : MemLp id p Œº) :
    (E ‚ÜíL[‚Ñù] ‚Ñù) ‚Üí‚Çó[‚Ñù] Lp ‚Ñù p Œº where
  toFun := fun L ‚Ü¶ MemLp.toLp L (h_Lp.continuousLinearMap L)
  map_add' u v := by push_cast; rw [MemLp.toLp_add]
  map_smul' c L := by push_cast; rw [MemLp.toLp_const_smul]; rfl

@[simp]
lemma ContinuousLinearMap.toLp‚Çó_apply (h_Lp : MemLp id p Œº) (L : E ‚ÜíL[‚Ñù] ‚Ñù) :
    L.toLp‚Çó Œº p h_Lp = MemLp.toLp L (h_Lp.continuousLinearMap L) := rfl

lemma norm_toLp‚Çó_le (h_Lp : MemLp id p Œº) (L : E ‚ÜíL[‚Ñù] ‚Ñù) (hp : p ‚â† 0) (hp_top : p ‚â† ‚àû) :
    ‚ÄñL.toLp‚Çó Œº p h_Lp‚Äñ ‚â§ ‚ÄñL‚Äñ * (eLpNorm id p Œº).toReal := by
  have h0 : 0 < p.toReal := by simp [ENNReal.toReal_pos_iff, pos_iff_ne_zero, hp, hp_top.lt_top]
  suffices ‚ÄñL.toLp‚Çó Œº p h_Lp‚Äñ
      ‚â§ (‚ÄñL‚Äñ‚Çë ^ p.toReal * ‚à´‚Åª x, ‚Äñx‚Äñ‚Çë ^ p.toReal ‚àÇŒº).toReal ^ p.toReal‚Åª¬π by
    refine this.trans_eq ?_
    simp only [ENNReal.toReal_mul]
    rw [‚Üê ENNReal.toReal_rpow, Real.mul_rpow (by positivity) (by positivity),
      ‚Üê Real.rpow_mul (by positivity), mul_inv_cancel‚ÇÄ h0.ne', Real.rpow_one, toReal_enorm]
    rw [eLpNorm_eq_lintegral_rpow_enorm (by simp [hp]) hp_top, ENNReal.toReal_rpow]
    simp
  rw [ContinuousLinearMap.toLp‚Çó_apply, Lp.norm_toLp,
    eLpNorm_eq_lintegral_rpow_enorm (by simp [hp]) hp_top]
  simp only [ENNReal.toReal_ofNat, ENNReal.rpow_ofNat, one_div]
  refine ENNReal.toReal_le_of_le_ofReal (by positivity) ?_
  suffices ‚à´‚Åª x, ‚ÄñL x‚Äñ‚Çë ^ p.toReal ‚àÇŒº ‚â§ ‚ÄñL‚Äñ‚Çë ^ p.toReal * ‚à´‚Åª x, ‚Äñx‚Äñ‚Çë ^ p.toReal ‚àÇŒº by
    rw [‚Üê ENNReal.ofReal_rpow_of_nonneg (by positivity) (by positivity)]
    gcongr
    rwa [ENNReal.ofReal_toReal]
    refine ENNReal.mul_ne_top (by simp) ?_
    have h := h_Lp.eLpNorm_ne_top
    rw [eLpNorm_eq_lintegral_rpow_enorm (by simp [hp]) hp_top] at h
    simpa [h0] using h
  calc ‚à´‚Åª x, ‚ÄñL x‚Äñ‚Çë ^ p.toReal ‚àÇŒº
  _ ‚â§ ‚à´‚Åª x, ‚ÄñL‚Äñ‚Çë ^ p.toReal * ‚Äñx‚Äñ‚Çë ^ p.toReal ‚àÇŒº := by
    refine lintegral_mono fun x ‚Ü¶ ?_
    rw [‚Üê ENNReal.mul_rpow_of_nonneg]
    swap; ¬∑ positivity
    gcongr
    simp_rw [‚Üê ofReal_norm]
    rw [‚Üê ENNReal.ofReal_mul (by positivity)]
    gcongr
    exact L.le_opNorm x
  _ = ‚ÄñL‚Äñ‚Çë ^ p.toReal * ‚à´‚Åª x, ‚Äñx‚Äñ‚Çë ^ p.toReal ‚àÇŒº := by rw [lintegral_const_mul]; fun_prop

/-- `MemLp.toLp` as a `ContinuousLinearMap` from the continuous linear maps. -/
def ContinuousLinearMap.toLp (Œº : Measure E) (p : ‚Ñù‚â•0‚àû) [Fact (1 ‚â§ p)] (h_Lp : MemLp id p Œº)
    (hp : p ‚â† ‚àû) :
    (E ‚ÜíL[‚Ñù] ‚Ñù) ‚ÜíL[‚Ñù] Lp ‚Ñù p Œº where
  toLinearMap := ContinuousLinearMap.toLp‚Çó Œº p h_Lp
  cont := by
    refine LinearMap.continuous_of_locally_bounded _ fun s hs ‚Ü¶ ?_
    rw [image_isVonNBounded_iff]
    simp_rw [isVonNBounded_iff'] at hs
    obtain ‚ü®r, hxr‚ü© := hs
    refine ‚ü®r * (eLpNorm id p Œº).toReal, fun L hLs ‚Ü¶ ?_‚ü©
    specialize hxr L hLs
    have hp_ne : p ‚â† 0 := by
      have : 1 ‚â§ p := Fact.out
      positivity
    refine (norm_toLp‚Çó_le h_Lp L hp_ne hp).trans ?_
    gcongr

@[simp]
lemma ContinuousLinearMap.toLp_apply (L : E ‚ÜíL[‚Ñù] ‚Ñù)
    [Fact (1 ‚â§ p)] (h_Lp : MemLp id p Œº) (hp : p ‚â† ‚àû) :
    L.toLp Œº p h_Lp hp = MemLp.toLp L (h_Lp.continuousLinearMap L) := rfl

end ToLp

section Mean

lemma _root_.ContinuousLinearMap.integral_comm_of_memLp_id [CompleteSpace E]
    (h_Lp : MemLp id 1 Œº) (L : E ‚ÜíL[‚Ñù] ‚Ñù) :
    Œº[L] = L (‚à´ x, x ‚àÇŒº) := by
  have h := L.integral_comp_L1_comm (h_Lp.toLp id)
  refine (trans ?_ h).trans ?_
  ¬∑ refine integral_congr_ae ?_
    filter_upwards [MemLp.coeFn_toLp h_Lp] with x hx
    rw [hx, id_eq]
  ¬∑ congr 1
    refine integral_congr_ae ?_
    filter_upwards [MemLp.coeFn_toLp h_Lp] with x hx
    rw [hx, id_eq]

end Mean

section Covariance

section Centered

variable [OpensMeasurableSpace E]

/-- Continuous bilinear form with value `‚à´ x, L‚ÇÅ x * L‚ÇÇ x ‚àÇŒº` on `(L‚ÇÅ, L‚ÇÇ)`.
This is the covariance only if `Œº` is centered. -/
noncomputable
def centeredCovariance (Œº : Measure E) (h : MemLp id 2 Œº) :
    (E ‚ÜíL[‚Ñù] ‚Ñù) ‚ÜíL[‚Ñù] (E ‚ÜíL[‚Ñù] ‚Ñù) ‚ÜíL[‚Ñù] ‚Ñù :=
  ContinuousLinearMap.bilinearComp (isBoundedBilinearMap_inner (ùïú := ‚Ñù)).toContinuousLinearMap
    (ContinuousLinearMap.toLp Œº 2 h (by simp)) (ContinuousLinearMap.toLp Œº 2 h (by simp))

lemma centeredCovariance_apply (h : MemLp id 2 Œº) (L‚ÇÅ L‚ÇÇ : E ‚ÜíL[‚Ñù] ‚Ñù) :
    centeredCovariance Œº h L‚ÇÅ L‚ÇÇ = ‚à´ x, L‚ÇÅ x * L‚ÇÇ x ‚àÇŒº := by
  simp only [centeredCovariance, ContinuousLinearMap.bilinearComp_apply,
    ContinuousLinearMap.toLp_apply, L2.inner_def,
    RCLike.inner_apply, conj_trivial]
  refine integral_congr_ae ?_
  filter_upwards [MemLp.coeFn_toLp (h.continuousLinearMap L‚ÇÅ),
    MemLp.coeFn_toLp (h.continuousLinearMap L‚ÇÇ)] with x hxL‚ÇÅ hxL‚ÇÇ
  rw [hxL‚ÇÅ, hxL‚ÇÇ, mul_comm]

lemma norm_centeredCovariance_le (h : MemLp id 2 Œº) (L‚ÇÅ L‚ÇÇ : E ‚ÜíL[‚Ñù] ‚Ñù) :
    ‚ÄñcenteredCovariance Œº h L‚ÇÅ L‚ÇÇ‚Äñ ‚â§ ‚ÄñL‚ÇÅ‚Äñ * ‚ÄñL‚ÇÇ‚Äñ * ‚à´ x, ‚Äñx‚Äñ ^ 2 ‚àÇŒº := by
  calc ‚ÄñcenteredCovariance Œº h L‚ÇÅ L‚ÇÇ‚Äñ
  _ = ‚Äñ‚à´ x, L‚ÇÅ x * L‚ÇÇ x ‚àÇŒº‚Äñ := by rw [centeredCovariance_apply]
  _ ‚â§ ‚à´ x, ‚ÄñL‚ÇÅ x‚Äñ * ‚ÄñL‚ÇÇ x‚Äñ ‚àÇŒº := (norm_integral_le_integral_norm _).trans (by simp)
  _ ‚â§ ‚à´ x, ‚ÄñL‚ÇÅ‚Äñ * ‚Äñx‚Äñ * ‚ÄñL‚ÇÇ‚Äñ * ‚Äñx‚Äñ ‚àÇŒº := by
    refine integral_mono_ae ?_ ?_ (ae_of_all _ fun x ‚Ü¶ ?_)
    ¬∑ simp_rw [‚Üê norm_mul]
      exact (MemLp.integrable_mul (h.continuousLinearMap L‚ÇÅ) (h.continuousLinearMap L‚ÇÇ)).norm
    ¬∑ simp_rw [mul_assoc]
      refine Integrable.const_mul ?_ _
      simp_rw [‚Üê mul_assoc, mul_comm _ (‚ÄñL‚ÇÇ‚Äñ), mul_assoc, ‚Üê pow_two]
      refine Integrable.const_mul ?_ _
      exact h.integrable_norm_pow (by simp)
    ¬∑ simp only
      rw [mul_assoc]
      gcongr
      ¬∑ exact ContinuousLinearMap.le_opNorm L‚ÇÅ x
      ¬∑ exact ContinuousLinearMap.le_opNorm L‚ÇÇ x
  _ = ‚ÄñL‚ÇÅ‚Äñ * ‚ÄñL‚ÇÇ‚Äñ * ‚à´ x, ‚Äñx‚Äñ ^ 2 ‚àÇŒº := by
    rw [‚Üê integral_const_mul]
    congr with x
    ring

lemma norm_centeredCovariance_le' (h : MemLp id 2 Œº) (L‚ÇÅ L‚ÇÇ : E ‚ÜíL[‚Ñù] ‚Ñù) :
    ‚ÄñcenteredCovariance Œº h L‚ÇÅ L‚ÇÇ‚Äñ ‚â§ ‚ÄñL‚ÇÅ‚Äñ * ‚ÄñL‚ÇÇ‚Äñ * (eLpNorm id 2 Œº).toReal ^ 2 := by
  calc ‚ÄñcenteredCovariance Œº h L‚ÇÅ L‚ÇÇ‚Äñ
  _ ‚â§ ‚ÄñL‚ÇÅ‚Äñ * ‚ÄñL‚ÇÇ‚Äñ * ‚à´ x, ‚Äñx‚Äñ ^ 2 ‚àÇŒº := norm_centeredCovariance_le _ _ _
  _ = ‚ÄñL‚ÇÅ‚Äñ * ‚ÄñL‚ÇÇ‚Äñ * (eLpNorm id 2 Œº).toReal ^ 2 := by
    congr
    have h := pow_toReal_eLpNorm h (by simp)
    simpa only [ENNReal.ofReal_ofNat, Real.rpow_two, id_eq] using h.symm

end Centered

variable [BorelSpace E] [SecondCountableTopology E] [CompleteSpace E]

/-- Continuous bilinear form with value `‚à´ x, (L‚ÇÅ x - Œº[L‚ÇÅ]) * (L‚ÇÇ x - Œº[L‚ÇÇ]) ‚àÇŒº` on `(L‚ÇÅ, L‚ÇÇ)`. -/
noncomputable
def covarianceBilin (Œº : Measure E) [IsFiniteMeasure Œº] (h : MemLp id 2 Œº) :
    (E ‚ÜíL[‚Ñù] ‚Ñù) ‚ÜíL[‚Ñù] (E ‚ÜíL[‚Ñù] ‚Ñù) ‚ÜíL[‚Ñù] ‚Ñù :=
  centeredCovariance (Œº.map (fun x ‚Ü¶ x - Œº[id])) <| by
    rw [memLp_map_measure_iff]
    ¬∑ exact h.sub (memLp_const _)
    ¬∑ exact Measurable.aestronglyMeasurable <| by fun_prop
    ¬∑ fun_prop

lemma covarianceBilin_apply [IsFiniteMeasure Œº] (h : MemLp id 2 Œº) (L‚ÇÅ L‚ÇÇ : E ‚ÜíL[‚Ñù] ‚Ñù) :
    covarianceBilin Œº h L‚ÇÅ L‚ÇÇ = ‚à´ x, (L‚ÇÅ x - Œº[L‚ÇÅ]) * (L‚ÇÇ x - Œº[L‚ÇÇ]) ‚àÇŒº := by
  rw [covarianceBilin, centeredCovariance_apply, integral_map]
  ¬∑ simp [‚Üê ContinuousLinearMap.integral_comm_of_memLp_id (h.mono_exponent (by simp))]
  ¬∑ fun_prop
  ¬∑ exact Measurable.aestronglyMeasurable <| by fun_prop

end Covariance

end ProbabilityTheory
