/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Probability.Moments.ComplexMGF
import Mathlib.Analysis.SpecialFunctions.Complex.Analytic

/-!
# The moment generating function is analytic

The moment generating function `mgf X Î¼` of a random variable `X` with respect to a measure `Î¼`
is analytic on the interior of `integrableExpSet X Î¼`, the interval on which it is defined.

## Main results

* `analyticOn_mgf`: the moment generating function is analytic on the interior of the interval
  on which it is defined.
* `iteratedDeriv_mgf`: the n-th derivative of the mgf at `t` is `Î¼[X ^ n * exp (t * X)]`.

-/


open MeasureTheory Filter Finset Real

open scoped MeasureTheory ProbabilityTheory ENNReal NNReal Topology Nat

namespace ProbabilityTheory

variable {Î© Î¹ : Type*} {m : MeasurableSpace Î©} {X : Î© â†’ â„} {Î¼ : Measure Î©} {t u v : â„}

section Deriv

/-- For `t : â„` with `t âˆˆ interior (integrableExpSet X Î¼)`, the derivative of the function
`x â†¦ Î¼[X ^ n * rexp (x * X)]` at `t` is `Î¼[X ^ (n + 1) * rexp (t * X)]`. -/
lemma hasDerivAt_integral_pow_mul_exp_real (ht : t âˆˆ interior (integrableExpSet X Î¼)) (n : â„•) :
    HasDerivAt (fun t â†¦ Î¼[fun Ï‰ â†¦ X Ï‰ ^ n * rexp (t * X Ï‰)])
      Î¼[fun Ï‰ â†¦ X Ï‰ ^ (n + 1) * rexp (t * X Ï‰)] t := by
  have h_re_of_mem n t (ht' : t âˆˆ interior (integrableExpSet X Î¼)) :
      (âˆ« Ï‰, X Ï‰ ^ n * Complex.exp (t * X Ï‰) âˆ‚Î¼).re = âˆ« Ï‰, X Ï‰ ^ n * rexp (t * X Ï‰) âˆ‚Î¼ := by
    rw [â† RCLike.re_eq_complex_re, â† integral_re]
    Â· norm_cast
    Â· refine integrable_pow_mul_cexp_of_re_mem_interior_integrableExpSet ?_ n
      simpa using ht'
  have h_re n : âˆ€á¶  t' : â„ in ğ“ t, (âˆ« Ï‰, X Ï‰ ^ n * Complex.exp (t' * X Ï‰) âˆ‚Î¼).re
      = âˆ« Ï‰, X Ï‰ ^ n * rexp (t' * X Ï‰) âˆ‚Î¼ := by
    filter_upwards [isOpen_interior.eventually_mem ht] with t ht' using h_re_of_mem n t ht'
  rw [â† EventuallyEq.hasDerivAt_iff (h_re _), â† h_re_of_mem _ t ht]
  exact (hasDerivAt_integral_pow_mul_exp (by simp [ht]) n).real_of_complex

/-- For `t âˆˆ interior (integrableExpSet X Î¼)`, the derivative of `mgf X Î¼` at `t` is
`Î¼[X * exp (t * X)]`. -/
lemma hasDerivAt_mgf (h : t âˆˆ interior (integrableExpSet X Î¼)) :
    HasDerivAt (mgf X Î¼) (Î¼[fun Ï‰ â†¦ X Ï‰ * exp (t * X Ï‰)]) t := by
  convert hasDerivAt_integral_pow_mul_exp_real h 0
  Â· simp [mgf]
  Â· simp

lemma hasDerivAt_iteratedDeriv_mgf (ht : t âˆˆ interior (integrableExpSet X Î¼)) (n : â„•) :
    HasDerivAt (iteratedDeriv n (mgf X Î¼)) Î¼[fun Ï‰ â†¦ X Ï‰ ^ (n + 1) * exp (t * X Ï‰)] t := by
  induction n generalizing t with
  | zero => simp [hasDerivAt_mgf ht]
  | succ n hn =>
    rw [iteratedDeriv_succ]
    have : deriv (iteratedDeriv n (mgf X Î¼))
        =á¶ [ğ“ t] fun t â†¦ Î¼[fun Ï‰ â†¦ X Ï‰ ^ (n + 1) * exp (t * X Ï‰)] := by
      have h_mem : âˆ€á¶  y in ğ“ t, y âˆˆ interior (integrableExpSet X Î¼) :=
        isOpen_interior.eventually_mem ht
      filter_upwards [h_mem] with y hy using HasDerivAt.deriv (hn hy)
    rw [EventuallyEq.hasDerivAt_iff this]
    exact hasDerivAt_integral_pow_mul_exp_real ht (n + 1)

/-- For `t âˆˆ interior (integrableExpSet X Î¼)`, the n-th derivative of `mgf X Î¼` at `t` is
`Î¼[X ^ n * exp (t * X)]`. -/
lemma iteratedDeriv_mgf (ht : t âˆˆ interior (integrableExpSet X Î¼)) (n : â„•) :
    iteratedDeriv n (mgf X Î¼) t = Î¼[fun Ï‰ â†¦ X Ï‰ ^ n * exp (t * X Ï‰)] := by
  induction n generalizing t with
  | zero => simp [mgf]
  | succ n hn =>
    rw [iteratedDeriv_succ]
    exact (hasDerivAt_iteratedDeriv_mgf ht n).deriv

/-- The derivatives of the moment generating function at zero are the moments. -/
lemma iteratedDeriv_mgf_zero (h : 0 âˆˆ interior (integrableExpSet X Î¼)) (n : â„•) :
    iteratedDeriv n (mgf X Î¼) 0 = Î¼[X ^ n] := by
  simp [iteratedDeriv_mgf h n]

/-- For `t âˆˆ interior (integrableExpSet X Î¼)`, the derivative of `mgf X Î¼` at `t` is
`Î¼[X * exp (t * X)]`. -/
lemma deriv_mgf (h : t âˆˆ interior (integrableExpSet X Î¼)) :
    deriv (mgf X Î¼) t = Î¼[fun Ï‰ â†¦ X Ï‰ * exp (t * X Ï‰)] :=
  (hasDerivAt_mgf h).deriv

lemma deriv_mgf_zero (h : 0 âˆˆ interior (integrableExpSet X Î¼)) : deriv (mgf X Î¼) 0 = Î¼[X] := by
  simp [deriv_mgf h]

end Deriv

section Analytic

/-- The moment generating function is analytic at every `t âˆˆ interior (integrableExpSet X Î¼)`. -/
lemma analyticAt_mgf (ht : t âˆˆ interior (integrableExpSet X Î¼)) :
    AnalyticAt â„ (mgf X Î¼) t := by
  rw [â† re_complexMGF_ofReal']
  exact (analyticAt_complexMGF (by simp [ht])).re_ofReal

lemma analyticOnNhd_mgf : AnalyticOnNhd â„ (mgf X Î¼) (interior (integrableExpSet X Î¼)) :=
  fun _ hx â†¦ analyticAt_mgf hx

/-- The moment generating function is analytic on the interior of the interval on which it is
defined. -/
lemma analyticOn_mgf : AnalyticOn â„ (mgf X Î¼) (interior (integrableExpSet X Î¼)) :=
  analyticOnNhd_mgf.analyticOn

lemma hasFPowerSeriesAt_mgf (hv : v âˆˆ interior (integrableExpSet X Î¼)) :
    HasFPowerSeriesAt (mgf X Î¼)
      (FormalMultilinearSeries.ofScalars â„
        (fun n â†¦ (Î¼[fun Ï‰ â†¦ X Ï‰ ^ n * exp (v * X Ï‰)] : â„) / n !)) v := by
  convert (analyticAt_mgf hv).hasFPowerSeriesAt
  rw [iteratedDeriv_mgf hv]

end Analytic

section GeneratingFunctionDerivatives

variable {X : Î© â†’ â„}

lemma integrable_expt_bound [IsFiniteMeasure Î¼] {t a b : â„} (hX : AEMeasurable X Î¼)
    (h : âˆ€áµ Ï‰ âˆ‚Î¼, X Ï‰ âˆˆ Set.Icc a b) :
    Integrable (fun Ï‰ â†¦ exp (t * (X Ï‰))) Î¼ := by
  cases lt_trichotomy t 0 with
  | inr ht => cases ht with
    | inr ht => exact integrable_exp_mul_of_le t b ht.le hX (h.mono fun Ï‰ h â†¦ h.2)
    | inl ht => rw [ht]; simp only [zero_mul, exp_zero, integrable_const]
  | inl ht =>
    rw [(by ext Ï‰; rw [(by ring : - t * (- X Ï‰) = t * X Ï‰)] :
      (fun Ï‰ â†¦ rexp (t * X Ï‰)) = (fun Ï‰ â†¦ rexp (- t * (- X Ï‰))))]
    apply integrable_exp_mul_of_le (-t) _ _ hX.neg
    Â· filter_upwards [h] with Ï‰ h using neg_le_neg h.1
    Â· linarith

lemma tilt_var_bound [IsProbabilityMeasure Î¼] (a b t : â„) (h : âˆ€áµ Ï‰ âˆ‚Î¼, X Ï‰ âˆˆ Set.Icc a b)
    (hX : AEMeasurable X Î¼) :
    variance X (Î¼.tilted (fun Ï‰ â†¦ t * X Ï‰)) â‰¤ ((b - a) / 2) ^ 2 := by
  have _ : IsProbabilityMeasure (Î¼.tilted fun Ï‰ â†¦ t * X Ï‰) :=
    isProbabilityMeasure_tilted (integrable_expt_bound hX h)
  exact variance_le_sq_of_bounded
    ((tilted_absolutelyContinuous Î¼ fun Ï‰ â†¦ t * X Ï‰) h)
    (hX.mono_ac (tilted_absolutelyContinuous Î¼ fun Ï‰ â†¦ t * X Ï‰))

lemma integrableExpSet_eq_univ_of_mem_Icc [IsFiniteMeasure Î¼] [NeZero Î¼] (a b : â„)
    (hX : AEMeasurable X Î¼) (h : âˆ€áµ Ï‰ âˆ‚Î¼, X Ï‰ âˆˆ Set.Icc a b) :
    integrableExpSet X Î¼ = Set.univ := by
  ext t
  simp only [Set.mem_univ, iff_true]
  exact integrable_expt_bound hX h

theorem integral_tilted' [IsFiniteMeasure Î¼] (t : â„) (f : â„ â†’ â„) :
    (Î¼.tilted (fun Ï‰ â†¦ t * X Ï‰))[fun Ï‰ â†¦ f (X Ï‰)] =
    (Î¼[fun Ï‰ â†¦ rexp (t * X Ï‰) * f (X Ï‰)]) / mgf X Î¼ t := by
  rw [MeasureTheory.integral_tilted, â† integral_div]
  simp only [smul_eq_mul, mgf]
  congr with Ï‰
  ring

/-! ### Derivatives of cumulant-/

/-- First derivative of cumulant `cgf X Î¼ f`.
It can be described by exponential tilting.-/
theorem cgf_deriv_one [IsFiniteMeasure Î¼] [NeZero Î¼] (a b : â„)
    {X : Î© â†’ â„} (hX : AEMeasurable X Î¼) (h : âˆ€áµ Ï‰ âˆ‚Î¼, X Ï‰ âˆˆ Set.Icc a b) (t : â„) :
    HasDerivAt (cgf X Î¼) ((Î¼.tilted (fun Ï‰ â†¦ t * X Ï‰))[X]) t := by
  have r0 : ((Î¼.tilted (fun Ï‰ â†¦ t * X Ï‰))[fun Ï‰ â†¦ id (X Ï‰)]) =
      Î¼[fun Ï‰ â†¦ rexp (t * X Ï‰) * id (X Ï‰)] / Î¼[fun Ï‰ â†¦ rexp (t * X Ï‰)] :=
    integral_tilted' t id
  simp only [id_eq] at r0
  rw [r0]
  apply HasDerivAt.log ?_ (mgf_pos' (NeZero.ne Î¼) (integrable_expt_bound hX h)).ne'
  convert hasDerivAt_mgf _ using 1
  Â· simp_rw [mul_comm]
  Â· simp [integrableExpSet_eq_univ_of_mem_Icc _ _ hX h]

/-- Second derivative of cumulant `cgf X Î¼ f`-/
theorem cgf_deriv_two [IsFiniteMeasure Î¼] [NeZero Î¼] (a b : â„)
    {X : Î© â†’ â„} (hX : AEMeasurable X Î¼) (h : âˆ€áµ Ï‰ âˆ‚Î¼, X Ï‰ âˆˆ Set.Icc a b) :
    let g' := fun t â†¦ (Î¼.tilted (fun Ï‰ â†¦ t * X Ï‰))[X];
    let g'' := fun t â†¦ (Î¼.tilted (fun Ï‰ â†¦ t * X Ï‰))[X ^ 2] - (Î¼.tilted (fun Ï‰ â†¦ t * X Ï‰))[X] ^ 2;
    âˆ€ x : â„, HasDerivAt g' (g'' x) x := by
  intro g' g'' t
  have r0 : (fun t â†¦ ((Î¼.tilted (fun Ï‰ â†¦ t * X Ï‰))[fun Ï‰ â†¦ id (X Ï‰)])) =
    fun t â†¦ Î¼[fun Ï‰ â†¦ rexp (t * X Ï‰) * id (X Ï‰)] / Î¼[fun Ï‰ â†¦ rexp (t * X Ï‰)] := by
    ext t
    exact integral_tilted' t id
  have r01 : (Î¼.tilted (fun Ï‰ â†¦ t * X Ï‰))[fun Ï‰ â†¦ id (X Ï‰)]  =
    Î¼[fun Ï‰ â†¦ rexp (t * X Ï‰) * id (X Ï‰)] / Î¼[fun Ï‰ â†¦ rexp (t * X Ï‰)] :=
    integral_tilted' t id
  have r0' : (Î¼.tilted (fun Ï‰ â†¦ t * X Ï‰))[fun Ï‰ â†¦ (fun s â†¦ s ^ 2) (X Ï‰)] =
    Î¼[fun Ï‰ â†¦ rexp (t * X Ï‰) * (fun s â†¦ s ^ 2) (X Ï‰)] / Î¼[fun Ï‰ â†¦ rexp (t * X Ï‰)] :=
    integral_tilted' t (fun x â†¦ x ^ 2)
  simp only [id_eq] at r0 r0' r01
  dsimp [g', g'']
  rw [r0, r0', r01]
  field_simp
  have p : ((Î¼[fun Ï‰ â†¦ rexp (t * X Ï‰) * X Ï‰ ^ 2]) / Î¼[fun Ï‰ â†¦ rexp (t * X Ï‰)]) =
  ((Î¼[fun Ï‰ â†¦ rexp (t * X Ï‰) * X Ï‰ ^ 2]) * (Î¼[fun Ï‰ â†¦ rexp (t * X Ï‰)])) /
  ((Î¼[fun Ï‰ â†¦ rexp (t * X Ï‰)]) * (Î¼[fun Ï‰ â†¦ rexp (t * X Ï‰)])) := by
    apply Eq.symm (mul_div_mul_right (âˆ« Ï‰, rexp (t * X Ï‰) * X Ï‰ ^ 2 âˆ‚Î¼)
    (Î¼[fun Ï‰ â†¦ rexp (t * X Ï‰)]) _)
    exact (mgf_pos' (NeZero.ne Î¼) (integrable_expt_bound hX h)).ne'
  rw [p, Eq.symm (pow_two (âˆ« Ï‰, rexp (t * X Ï‰) âˆ‚Î¼))]
  have p'' : (((Î¼[fun Ï‰ â†¦ rexp (t * X Ï‰) * X Ï‰ ^ 2]) *
    (Î¼[fun Ï‰ â†¦ rexp (t * X Ï‰)])) / (Î¼[fun Ï‰ â†¦ rexp (t * X Ï‰)]) ^ 2 -
  (Î¼[fun Ï‰ â†¦ rexp (t * X Ï‰) * X Ï‰]) ^ 2 / (Î¼[fun Ï‰ â†¦ rexp (t * X Ï‰)]) ^ 2) =
  ((Î¼[fun Ï‰ â†¦ exp (t * (X Ï‰)) * (X Ï‰) ^ 2] *
    mgf X Î¼ t) -
    (Î¼[fun Ï‰ â†¦ exp (t * (X Ï‰)) * X Ï‰] * Î¼[fun Ï‰ â†¦ exp (t * (X Ï‰)) * X Ï‰])) /
    (mgf X Î¼ t ^ 2) := by
    rw [Eq.symm (pow_two (âˆ« Ï‰, (fun Ï‰ â†¦ rexp (t * X Ï‰) * X Ï‰) Ï‰ âˆ‚Î¼))]
    exact
      div_sub_div_same ((Î¼[fun Ï‰ â†¦ rexp (t * X Ï‰) * X Ï‰ ^ 2]) * Î¼[fun Ï‰ â†¦ rexp (t * X Ï‰)])
        ((Î¼[fun Ï‰ â†¦ rexp (t * X Ï‰) * X Ï‰]) ^ 2) ((Î¼[fun Ï‰ â†¦ rexp (t * X Ï‰)]) ^ 2)
  rw [p'']
  apply HasDerivAt.div
  Â· set c := max â€–aâ€– â€–bâ€–
    convert hasDerivAt_integral_pow_mul_exp_real (X := X) (Î¼ := Î¼) ?_ 1 using 1
    Â· simp only [pow_one, g'', g']
      simp_rw [mul_comm]
    Â· simp only [Nat.reduceAdd, g'', g']
      simp_rw [mul_comm]
    Â· simp [integrableExpSet_eq_univ_of_mem_Icc _ _ hX h]
  Â· convert hasDerivAt_integral_pow_mul_exp_real (X := X) (Î¼ := Î¼) ?_ 0 using 1
    Â· simp
    Â· simp only [zero_add, pow_one, g'', g']
      simp_rw [mul_comm]
    Â· simp [integrableExpSet_eq_univ_of_mem_Icc _ _ hX h]
  Â· exact (mgf_pos' (NeZero.ne Î¼) (integrable_expt_bound hX h)).ne'

theorem cgf_zero_deriv [IsProbabilityMeasure Î¼] {X : Î© â†’ â„} (h0 : Î¼[X] = 0) :
    let f' := fun t â†¦ âˆ« (x : Î©), X x âˆ‚Measure.tilted Î¼ fun Ï‰ â†¦ t * X Ï‰;
  f' 0 = 0 := by
  simp only [zero_mul, tilted_const', measure_univ, inv_one, one_smul]
  exact h0

end GeneratingFunctionDerivatives

end ProbabilityTheory
