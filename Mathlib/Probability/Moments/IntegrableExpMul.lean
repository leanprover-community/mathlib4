/-
Copyright (c) 2024 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Function.L1Space.Integrable
import Mathlib.MeasureTheory.Order.Group.Lattice

/-!
# Domain of the moment generating function

For `X` a real random variable and `Î¼` a finite measure, the set
`{t | Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼}` is an interval containing zero. This is the set of
points for which the moment generating function `mgf X Î¼ t` is well defined.
We denote that set by `integrableExpSet X Î¼`.

We prove the integrability of other functions for `t` in the interior of that interval.

## Main definitions

* `ProbabilityTheory.IntegrableExpSet`: the interval of reals for which `exp (t * X)` is integrable.

## Main results

* `ProbabilityTheory.integrable_exp_mul_of_le_of_le`: if `exp (u * X)` is integrable for `u = a` and
  `u = b` then it is integrable on `[a, b]`.
* `ProbabilityTheory.convex_integrableExpSet`: `integrableExpSet X Î¼` is a convex set.
* `ProbabilityTheory.integrable_exp_mul_of_nonpos_of_ge`: if `exp (u * X)` is integrable for `u â‰¤ 0`
  then it is integrable on `[u, 0]`.
* `ProbabilityTheory.integrable_rpow_abs_mul_exp_of_mem_interior`: for `v` in the interior of the
  interval in which `exp (t * X)` is integrable, for all nonnegative `p : â„`,
  `|X| ^ p * exp (v * X)` is integrable.
* `ProbabilityTheory.memLp_of_mem_interior_integrableExpSet`: if 0 belongs to the interior of
  `integrableExpSet X Î¼`, then `X` is in `â„’p` for all finite `p`.

-/


open MeasureTheory Filter Finset Real

open scoped MeasureTheory ProbabilityTheory ENNReal NNReal Topology

namespace ProbabilityTheory

variable {Î© Î¹ : Type*} {m : MeasurableSpace Î©} {X : Î© â†’ â„} {Î¼ : Measure Î©} {t u v : â„}

section Interval

lemma integrable_exp_mul_of_le_of_le {a b : â„}
    (ha : Integrable (fun Ï‰ â†¦ exp (a * X Ï‰)) Î¼) (hb : Integrable (fun Ï‰ â†¦ exp (b * X Ï‰)) Î¼)
    (hat : a â‰¤ t) (htb : t â‰¤ b) :
    Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼ := by
  refine Integrable.mono (ha.add hb) ?_ (ae_of_all _ fun Ï‰ â†¦ ?_)
  Â· by_cases hab : a = b
    Â· have ha_eq_t : a = t := le_antisymm hat (hab â–¸ htb)
      rw [â† ha_eq_t]
      exact ha.1
    Â· refine AEMeasurable.aestronglyMeasurable ?_
      refine measurable_exp.comp_aemeasurable (AEMeasurable.const_mul ?_ _)
      by_cases ha_zero : a = 0
      Â· refine aemeasurable_of_aemeasurable_exp_mul ?_ hb.1.aemeasurable
        rw [ha_zero] at hab
        exact Ne.symm hab
      Â· exact aemeasurable_of_aemeasurable_exp_mul ha_zero ha.1.aemeasurable
  Â· simp only [norm_eq_abs, abs_exp, Pi.add_apply]
    conv_rhs => rw [abs_of_nonneg (by positivity)]
    rcases le_total 0 (X Ï‰) with h | h
    Â· calc exp (t * X Ï‰)
      _ â‰¤ exp (b * X Ï‰) := exp_le_exp.mpr (mul_le_mul_of_nonneg_right htb h)
      _ â‰¤ exp (a * X Ï‰) + exp (b * X Ï‰) := le_add_of_nonneg_left (exp_nonneg _)
    Â· calc exp (t * X Ï‰)
      _ â‰¤ exp (a * X Ï‰) := exp_le_exp.mpr (mul_le_mul_of_nonpos_right hat h)
      _ â‰¤ exp (a * X Ï‰) + exp (b * X Ï‰) := le_add_of_nonneg_right (exp_nonneg _)

/-- If `Ï‰ â†¦ exp (u * X Ï‰)` is integrable at `u` and `-u`, then it is integrable on `[-u, u]`. -/
lemma integrable_exp_mul_of_abs_le
    (hu_int_pos : Integrable (fun Ï‰ â†¦ exp (u * X Ï‰)) Î¼)
    (hu_int_neg : Integrable (fun Ï‰ â†¦ exp (- u * X Ï‰)) Î¼)
    (htu : |t| â‰¤ |u|) :
    Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼ := by
  refine integrable_exp_mul_of_le_of_le (a := -|u|) (b := |u|) ?_ ?_ ?_ ?_
  Â· rcases le_total 0 u with hu | hu
    Â· rwa [abs_of_nonneg hu]
    Â· simpa [abs_of_nonpos hu]
  Â· rcases le_total 0 u with hu | hu
    Â· rwa [abs_of_nonneg hu]
    Â· rwa [abs_of_nonpos hu]
  Â· rw [neg_le]
    exact (neg_le_abs t).trans htu
  Â· exact (le_abs_self t).trans htu

/-- If `Ï‰ â†¦ exp (u * X Ï‰)` is integrable at `u â‰¥ 0`, then it is integrable on `[0, u]`. -/
lemma integrable_exp_mul_of_nonneg_of_le [IsFiniteMeasure Î¼]
    (hu : Integrable (fun Ï‰ â†¦ exp (u * X Ï‰)) Î¼) (h_nonneg : 0 â‰¤ t) (htu : t â‰¤ u) :
    Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼ :=
  integrable_exp_mul_of_le_of_le (by simp) hu h_nonneg htu

/-- If `Ï‰ â†¦ exp (u * X Ï‰)` is integrable at `u â‰¤ 0`, then it is integrable on `[u, 0]`. -/
lemma integrable_exp_mul_of_nonpos_of_ge [IsFiniteMeasure Î¼]
    (hu : Integrable (fun Ï‰ â†¦ exp (u * X Ï‰)) Î¼) (h_nonpos : t â‰¤ 0) (htu : u â‰¤ t) :
    Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼ :=
  integrable_exp_mul_of_le_of_le hu (by simp) htu h_nonpos

end Interval

section IntegrableExpSet

/-- The interval of reals `t` for which `exp (t * X)` is integrable. -/
def integrableExpSet (X : Î© â†’ â„) (Î¼ : Measure Î©) : Set â„ :=
  {t | Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼}

lemma integrable_of_mem_integrableExpSet (h : t âˆˆ integrableExpSet X Î¼) :
    Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼ := h

/-- `integrableExpSet X Î¼` is a convex subset of `â„` (it is an interval). -/
lemma convex_integrableExpSet : Convex â„ (integrableExpSet X Î¼) := by
  rintro tâ‚ htâ‚ tâ‚‚ htâ‚‚ a b ha hb hab
  wlog h_le : tâ‚ â‰¤ tâ‚‚
  Â· rw [add_comm] at hab âŠ¢
    exact this htâ‚‚ htâ‚ hb ha hab (not_le.mp h_le).le
  refine integrable_exp_mul_of_le_of_le htâ‚ htâ‚‚ ?_ ?_
  Â· simp only [smul_eq_mul]
    calc tâ‚
    _ = a * tâ‚ + b * tâ‚ := by rw [â† add_mul, hab, one_mul]
    _ â‰¤ a * tâ‚ + b * tâ‚‚ := by gcongr
  Â· simp only [smul_eq_mul]
    calc a * tâ‚ + b * tâ‚‚
    _ â‰¤ a * tâ‚‚ + b * tâ‚‚ := by gcongr
    _ = tâ‚‚ := by rw [â† add_mul, hab, one_mul]

end IntegrableExpSet

section FiniteMoments

lemma aemeasurable_of_integrable_exp_mul (huv : u â‰  v)
    (hu_int : Integrable (fun Ï‰ â†¦ exp (u * X Ï‰)) Î¼)
    (hv_int : Integrable (fun Ï‰ â†¦ exp (v * X Ï‰)) Î¼) :
    AEMeasurable X Î¼ := by
  by_cases hu : u = 0
  Â· have hv : v â‰  0 := ne_of_ne_of_eq huv.symm hu
    exact aemeasurable_of_aemeasurable_exp_mul hv hv_int.aemeasurable
  Â· exact aemeasurable_of_aemeasurable_exp_mul hu hu_int.aemeasurable

/-- If `exp ((v + t) * X)` and `exp ((v - t) * X)` are integrable, then
`Ï‰ â†¦ exp (t * |X| + v * X)` is integrable. -/
lemma integrable_exp_mul_abs_add (ht_int_pos : Integrable (fun Ï‰ â†¦ exp ((v + t) * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ exp ((v - t) * X Ï‰)) Î¼) :
    Integrable (fun Ï‰ â†¦ exp (t * |X Ï‰| + v * X Ï‰)) Î¼ := by
  have h_int_add : Integrable (fun a â†¦ exp ((v + t) * X a) + exp ((v - t) * X a)) Î¼ :=
    ht_int_pos.add <| by simpa using ht_int_neg
  refine Integrable.mono h_int_add ?_ (ae_of_all _ fun Ï‰ â†¦ ?_)
  Â· by_cases ht : t = 0
    Â· simp only [ht, zero_mul, zero_add, add_zero] at ht_int_pos âŠ¢
      exact ht_int_pos.1
    have hX : AEMeasurable X Î¼ := aemeasurable_of_integrable_exp_mul ?_ ht_int_pos ht_int_neg
    swap; Â· rw [â† sub_ne_zero]; simp [ht]
    refine AEMeasurable.aestronglyMeasurable ?_
    exact measurable_exp.comp_aemeasurable ((hX.abs.const_mul _).add (hX.const_mul _))
  Â· simp only [norm_eq_abs, abs_exp]
    conv_rhs => rw [abs_of_nonneg (by positivity)]
    -- âŠ¢ exp (t * |X Ï‰| + v * X Ï‰) â‰¤ exp ((v + t) * X Ï‰) + exp ((v - t) * X Ï‰)
    rcases le_total 0 (X Ï‰) with h_nonneg | h_nonpos
    Â· rw [abs_of_nonneg h_nonneg, â† add_mul, add_comm, le_add_iff_nonneg_right]
      positivity
    Â· rw [abs_of_nonpos h_nonpos, mul_neg, mul_comm, â† mul_neg, mul_comm, â† add_mul, add_comm,
        â† sub_eq_add_neg, le_add_iff_nonneg_left]
      positivity

/-- If `Ï‰ â†¦ exp (t * X Ï‰)` is integrable at `t` and `-t`, then `Ï‰ â†¦ exp (t * |X Ï‰|)` is
integrable. -/
lemma integrable_exp_mul_abs (ht_int_pos : Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ exp (- t * X Ï‰)) Î¼) :
    Integrable (fun Ï‰ â†¦ exp (t * |X Ï‰|)) Î¼ := by
  have h := integrable_exp_mul_abs_add (t := t) (Î¼ := Î¼) (X := X) (v := 0) ?_ ?_
  Â· simpa using h
  Â· simpa using ht_int_pos
  Â· simpa using ht_int_neg

/-- If `exp ((v + t) * X)` and `exp ((v - t) * X)` are integrable, then
`Ï‰ â†¦ exp (|t| * |X| + v * X)` is integrable. -/
lemma integrable_exp_abs_mul_abs_add (ht_int_pos : Integrable (fun Ï‰ â†¦ exp ((v + t) * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ exp ((v - t) * X Ï‰)) Î¼) :
    Integrable (fun Ï‰ â†¦ exp (|t| * |X Ï‰| + v * X Ï‰)) Î¼ := by
  rcases le_total 0 t with ht_nonneg | ht_nonpos
  Â· simp_rw [abs_of_nonneg ht_nonneg]
    exact integrable_exp_mul_abs_add ht_int_pos ht_int_neg
  Â· simp_rw [abs_of_nonpos ht_nonpos]
    exact integrable_exp_mul_abs_add ht_int_neg (by simpa using ht_int_pos)

/-- If `Ï‰ â†¦ exp (t * X Ï‰)` is integrable at `t` and `-t`, then `Ï‰ â†¦ exp (|t| * |X Ï‰|)` is
integrable. -/
lemma integrable_exp_abs_mul_abs (ht_int_pos : Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ exp (- t * X Ï‰)) Î¼) :
    Integrable (fun Ï‰ â†¦ exp (|t| * |X Ï‰|)) Î¼ := by
  rcases le_total 0 t with ht_nonneg | ht_nonpos
  Â· simp_rw [abs_of_nonneg ht_nonneg]
    exact integrable_exp_mul_abs ht_int_pos ht_int_neg
  Â· simp_rw [abs_of_nonpos ht_nonpos]
    exact integrable_exp_mul_abs ht_int_neg (by simpa using ht_int_pos)

/-- Auxiliary lemma for `rpow_abs_le_mul_max_exp`. -/
lemma rpow_abs_le_mul_max_exp_of_pos (x : â„) {t p : â„} (hp : 0 â‰¤ p) (ht : 0 < t) :
    |x| ^ p â‰¤ (p / t) ^ p * max (exp (t * x)) (exp (- t * x)) := by
  by_cases hp_zero : p = 0
  Â· simp only [hp_zero, rpow_zero, zero_div, neg_mul, one_mul, le_sup_iff, one_le_exp_iff,
      Left.nonneg_neg_iff]
    exact le_total 0 (t * x)
  have h_x_le c (hc : 0 < c) : x â‰¤ câ»Â¹ * exp (c * x) := le_inv_mul_exp x hc
  have h_neg_x_le c (hc : 0 < c) : -x â‰¤ câ»Â¹ * exp (- c * x) := by simpa using le_inv_mul_exp (-x) hc
  have h_abs_le c (hc : 0 < c) : |x| â‰¤ câ»Â¹ * max (exp (c * x)) (exp (- c * x)) := by
    refine abs_le.mpr âŸ¨?_, ?_âŸ©
    Â· rw [neg_le]
      refine (h_neg_x_le c hc).trans ?_
      gcongr
      exact le_max_right _ _
    Â· refine (h_x_le c hc).trans ?_
      gcongr
      exact le_max_left _ _
  calc |x| ^ p
  _ â‰¤ ((t / p)â»Â¹ * max (exp (t / p * x)) (exp (- t / p * x))) ^ p := by
    refine rpow_le_rpow (abs_nonneg _) ?_ hp
    convert h_abs_le (t / p) (div_pos ht (hp.lt_of_ne' hp_zero)) using 5
    rw [neg_div]
  _ = (p / t) ^ p * max (exp (t * x)) (exp (- t * x)) := by
    rw [mul_rpow (by positivity) (by positivity)]
    congr
    Â· field_simp
    Â· rw [rpow_max (by positivity) (by positivity) hp, â† exp_mul, â† exp_mul]
      ring_nf
      congr <;> rw [mul_assoc, mul_inv_cancelâ‚€ hp_zero, mul_one]

lemma rpow_abs_le_mul_max_exp (x : â„) {t p : â„} (hp : 0 â‰¤ p) (ht : t â‰  0) :
    |x| ^ p â‰¤ (p / |t|) ^ p * max (exp (t * x)) (exp (- t * x)) := by
  rcases lt_or_gt_of_ne ht with ht_neg | ht_pos
  Â· rw [abs_of_nonpos ht_neg.le, sup_comm]
    convert rpow_abs_le_mul_max_exp_of_pos x hp (t := -t) (by simp [ht_neg])
    simp
  Â· rw [abs_of_nonneg ht_pos.le]
    exact rpow_abs_le_mul_max_exp_of_pos x hp ht_pos

lemma rpow_abs_le_mul_exp_abs (x : â„) {t p : â„} (hp : 0 â‰¤ p) (ht : t â‰  0) :
    |x| ^ p â‰¤ (p / |t|) ^ p * exp (|t| * |x|) := by
  refine (rpow_abs_le_mul_max_exp_of_pos x hp (t := |t|) ?_).trans_eq ?_
  Â· simp [ht]
  Â· congr
    rcases le_total 0 x with hx | hx
    Â· rw [abs_of_nonneg hx]
      simp only [neg_mul, sup_eq_left, exp_le_exp, neg_le_self_iff]
      positivity
    Â· rw [abs_of_nonpos hx]
      simp only [neg_mul, mul_neg, sup_eq_right, exp_le_exp, le_neg_self_iff]
      exact mul_nonpos_of_nonneg_of_nonpos (abs_nonneg _) hx

/-- If `exp ((v + t) * X)` and `exp ((v - t) * X)` are integrable
then for nonnegative `p : â„` and any `x âˆˆ [0, |t|)`,
`|X| ^ p * exp (v * X + x * |X|)` is integrable. -/
lemma integrable_rpow_abs_mul_exp_add_of_integrable_exp_mul {x : â„}
    (h_int_pos : Integrable (fun Ï‰ â†¦ exp ((v + t) * X Ï‰)) Î¼)
    (h_int_neg : Integrable (fun Ï‰ â†¦ exp ((v - t) * X Ï‰)) Î¼) (h_nonneg : 0 â‰¤ x) (hx : x < |t|)
    {p : â„} (hp : 0 â‰¤ p) :
    Integrable (fun a â†¦ |X a| ^ p * exp (v * X a + x * |X a|)) Î¼ := by
  have ht : t â‰  0 := by
    suffices |t| â‰  0 by simpa
    exact (h_nonneg.trans_lt hx).ne'
  have hX : AEMeasurable X Î¼ := aemeasurable_of_integrable_exp_mul ?_ h_int_pos h_int_neg
  swap; Â· rw [â† sub_ne_zero]; simp [ht]
  rw [â† integrable_norm_iff]
  swap
  Â· exact AEMeasurable.aestronglyMeasurable <| by fun_prop
  simp only [norm_mul, norm_pow, norm_eq_abs, sq_abs, abs_exp]
  have h_le a : |X a| ^ p * exp (v * X a + x * |X a|)
      â‰¤ (p / (|t| - x)) ^ p * exp (v * X a + |t| * |X a|) := by
    simp_rw [exp_add, mul_comm (exp (v * X a)), â† mul_assoc]
    gcongr ?_ * _
    have : |t| = |t| - x + x := by simp
    nth_rw 2 [this]
    rw [add_mul, exp_add, â† mul_assoc]
    gcongr ?_ * _
    convert rpow_abs_le_mul_exp_abs (X a) hp (t := |t| - x) _ using 4
    Â· nth_rw 2 [abs_of_nonneg]
      simp [hx.le]
    Â· nth_rw 2 [abs_of_nonneg]
      simp [hx.le]
    Â· rw [sub_ne_zero]
      exact hx.ne'
  refine Integrable.mono (g := fun a â†¦ (p / (|t| - x)) ^ p * exp (v * X a + |t| * |X a|))
    ?_ ?_ <| ae_of_all _ fun Ï‰ â†¦ ?_
  Â· refine Integrable.const_mul ?_ _
    simp_rw [add_comm (v * X _)]
    exact integrable_exp_abs_mul_abs_add h_int_pos h_int_neg
  Â· refine AEMeasurable.aestronglyMeasurable ?_
    exact AEMeasurable.mul (by fun_prop) (measurable_exp.comp_aemeasurable (by fun_prop))
  Â· simp only [sq_abs, norm_mul, norm_pow, norm_eq_abs, abs_exp, norm_div, norm_ofNat]
    simp_rw [abs_rpow_of_nonneg (abs_nonneg _), abs_abs]
    refine (h_le Ï‰).trans_eq ?_
    congr
    symm
    simp only [abs_eq_self, sub_nonneg]
    exact rpow_nonneg (div_nonneg hp (sub_nonneg_of_le hx.le)) _

/-- If `exp ((v + t) * X)` and `exp ((v - t) * X)` are integrable
then for any `n : â„•` and any `x âˆˆ [0, |t|)`,
`|X| ^ n * exp (v * X + x * |X|)` is integrable. -/
lemma integrable_pow_abs_mul_exp_add_of_integrable_exp_mul {x : â„}
    (h_int_pos : Integrable (fun Ï‰ â†¦ exp ((v + t) * X Ï‰)) Î¼)
    (h_int_neg : Integrable (fun Ï‰ â†¦ exp ((v - t) * X Ï‰)) Î¼) (h_nonneg : 0 â‰¤ x) (hx : x < |t|)
    (n : â„•) :
    Integrable (fun a â†¦ |X a| ^ n * exp (v * X a + x * |X a|)) Î¼ := by
  convert integrable_rpow_abs_mul_exp_add_of_integrable_exp_mul h_int_pos h_int_neg h_nonneg hx
    n.cast_nonneg
  simp

/-- If `exp ((v + t) * X)` and `exp ((v - t) * X)` are integrable
then for nonnegative `p : â„`, `|X| ^ p * exp (v * X)` is integrable. -/
lemma integrable_rpow_abs_mul_exp_of_integrable_exp_mul (ht : t â‰  0)
    (ht_int_pos : Integrable (fun Ï‰ â†¦ exp ((v + t) * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ exp ((v - t) * X Ï‰)) Î¼) {p : â„} (hp : 0 â‰¤ p) :
    Integrable (fun Ï‰ â†¦ |X Ï‰| ^ p * exp (v * X Ï‰)) Î¼ := by
  convert integrable_rpow_abs_mul_exp_add_of_integrable_exp_mul ht_int_pos ht_int_neg le_rfl _ hp
    using 4
  Â· simp
  Â· simp [ht]

/-- If `exp ((v + t) * X)` and `exp ((v - t) * X)` are integrable, then for all `n : â„•`,
`|X| ^ n * exp (v * X)` is integrable. -/
lemma integrable_pow_abs_mul_exp_of_integrable_exp_mul (ht : t â‰  0)
    (ht_int_pos : Integrable (fun Ï‰ â†¦ exp ((v + t) * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ exp ((v - t) * X Ï‰)) Î¼) (n : â„•) :
    Integrable (fun Ï‰ â†¦ |X Ï‰| ^ n * exp (v * X Ï‰)) Î¼ := by
  convert integrable_rpow_abs_mul_exp_of_integrable_exp_mul ht ht_int_pos ht_int_neg
    (by positivity : 0 â‰¤ (n : â„)) with Ï‰
  simp

/-- If `exp ((v + t) * X)` and `exp ((v - t) * X)` are integrable, then for all nonnegative `p : â„`,
`X ^ p * exp (v * X)` is integrable. -/
lemma integrable_rpow_mul_exp_of_integrable_exp_mul (ht : t â‰  0)
    (ht_int_pos : Integrable (fun Ï‰ â†¦ exp ((v + t) * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ exp ((v - t) * X Ï‰)) Î¼) {p : â„} (hp : 0 â‰¤ p) :
    Integrable (fun Ï‰ â†¦ X Ï‰ ^ p * exp (v * X Ï‰)) Î¼ := by
  have hX : AEMeasurable X Î¼ := aemeasurable_of_integrable_exp_mul ?_ ht_int_pos ht_int_neg
  swap; Â· rw [â† sub_ne_zero]; simp [ht]
  rw [â† integrable_norm_iff]
  Â· simp_rw [norm_eq_abs, abs_mul, abs_exp]
    have h := integrable_rpow_abs_mul_exp_of_integrable_exp_mul ht ht_int_pos ht_int_neg hp
    refine h.mono' ?_ ?_
    Â· exact ((hX.pow_const _).abs.mul
        (measurable_exp.comp_aemeasurable (hX.const_mul _))).aestronglyMeasurable
    Â· refine ae_of_all _ fun Ï‰ â†¦ ?_
      simp only [norm_mul, norm_eq_abs, abs_abs, abs_exp]
      gcongr
      exact abs_rpow_le_abs_rpow _ _
  Â· exact ((hX.pow_const _).mul
      (measurable_exp.comp_aemeasurable (hX.const_mul _))).aestronglyMeasurable

/-- If `exp ((v + t) * X)` and `exp ((v - t) * X)` are integrable, then for all `n : â„•`,
`X ^ n * exp (v * X)` is integrable. -/
lemma integrable_pow_mul_exp_of_integrable_exp_mul (ht : t â‰  0)
    (ht_int_pos : Integrable (fun Ï‰ â†¦ exp ((v + t) * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ exp ((v - t) * X Ï‰)) Î¼) (n : â„•) :
    Integrable (fun Ï‰ â†¦ X Ï‰ ^ n * exp (v * X Ï‰)) Î¼ := by
  convert integrable_rpow_mul_exp_of_integrable_exp_mul ht ht_int_pos ht_int_neg
    (by positivity : 0 â‰¤ (n : â„)) with Ï‰
  simp

/-- If `Ï‰ â†¦ exp (t * X Ï‰)` is integrable at `t` and `-t` for `t â‰  0`, then `Ï‰ â†¦ |X Ï‰| ^ p` is
integrable for all nonnegative `p : â„`. -/
lemma integrable_rpow_abs_of_integrable_exp_mul (ht : t â‰  0)
    (ht_int_pos : Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ exp (- t * X Ï‰)) Î¼) {p : â„} (hp : 0 â‰¤ p) :
    Integrable (fun Ï‰ â†¦ |X Ï‰| ^ p) Î¼ := by
  have h := integrable_rpow_abs_mul_exp_of_integrable_exp_mul (Î¼ := Î¼) (X := X) ht (v := 0) ?_ ?_ hp
  Â· simpa using h
  Â· simpa using ht_int_pos
  Â· simpa using ht_int_neg

/-- If `Ï‰ â†¦ exp (t * X Ï‰)` is integrable at `t` and `-t` for `t â‰  0`, then `Ï‰ â†¦ |X Ï‰| ^ n` is
integrable for all `n : â„•`. That is, all moments of `X` are finite. -/
lemma integrable_pow_abs_of_integrable_exp_mul (ht : t â‰  0)
    (ht_int_pos : Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ exp (- t * X Ï‰)) Î¼) (n : â„•) :
    Integrable (fun Ï‰ â†¦ |X Ï‰| ^ n) Î¼ := by
  convert integrable_rpow_abs_of_integrable_exp_mul ht ht_int_pos ht_int_neg
    (by positivity : 0 â‰¤ (n : â„)) with Ï‰
  simp

/-- If `Ï‰ â†¦ exp (t * X Ï‰)` is integrable at `t` and `-t` for `t â‰  0`, then `Ï‰ â†¦ X Ï‰ ^ p` is
integrable for all nonnegative `p : â„`. -/
lemma integrable_rpow_of_integrable_exp_mul (ht : t â‰  0)
    (ht_int_pos : Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ exp (- t * X Ï‰)) Î¼) {p : â„} (hp : 0 â‰¤ p) :
    Integrable (fun Ï‰ â†¦ X Ï‰ ^ p) Î¼ := by
  have h := integrable_rpow_mul_exp_of_integrable_exp_mul (Î¼ := Î¼) (X := X) ht (v := 0) ?_ ?_ hp
  Â· simpa using h
  Â· simpa using ht_int_pos
  Â· simpa using ht_int_neg

/-- If `Ï‰ â†¦ exp (t * X Ï‰)` is integrable at `t` and `-t` for `t â‰  0`, then `Ï‰ â†¦ X Ï‰ ^ n` is
integrable for all `n : â„•`. -/
lemma integrable_pow_of_integrable_exp_mul (ht : t â‰  0)
    (ht_int_pos : Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ exp (- t * X Ï‰)) Î¼) (n : â„•) :
    Integrable (fun Ï‰ â†¦ X Ï‰ ^ n) Î¼ := by
  convert integrable_rpow_of_integrable_exp_mul ht ht_int_pos ht_int_neg
    (by positivity : 0 â‰¤ (n : â„)) with Ï‰
  simp

section IntegrableExpSet

lemma add_half_inf_sub_mem_Ioo {l u v : â„} (hv : v âˆˆ Set.Ioo l u) :
    v + ((v - l) âŠ“ (u - v)) / 2 âˆˆ Set.Ioo l u := by
  have h_pos : 0 < (v - l) âŠ“ (u - v) := by simp [hv.1, hv.2]
  constructor
  Â· calc l < v := hv.1
    _ â‰¤ v + ((v - l) âŠ“ (u - v)) / 2 := le_add_of_nonneg_right (by positivity)
  Â· calc v + ((v - l) âŠ“ (u - v)) / 2
    _ < v + ((v - l) âŠ“ (u - v)) := by gcongr; exact half_lt_self (by positivity)
    _ â‰¤ v + (u - v) := by gcongr; exact inf_le_right
    _ = u := by abel

lemma sub_half_inf_sub_mem_Ioo {l u v : â„} (hv : v âˆˆ Set.Ioo l u) :
    v - ((v - l) âŠ“ (u - v)) / 2 âˆˆ Set.Ioo l u := by
  have h_pos : 0 < (v - l) âŠ“ (u - v) := by simp [hv.1, hv.2]
  constructor
  Â· calc l = v - (v - l) := by abel
    _ â‰¤ v - ((v - l) âŠ“ (u - v)) := by gcongr; exact inf_le_left
    _ < v - ((v - l) âŠ“ (u - v)) / 2 := by gcongr; exact half_lt_self (by positivity)
  Â· calc v - ((v - l) âŠ“ (u - v)) / 2
    _ â‰¤ v := by
      rw [sub_le_iff_le_add]
      exact le_add_of_nonneg_right (by positivity)
    _ < u := hv.2

/-- If the interior of the interval `integrableExpSet X Î¼` is nonempty,
then `X` is a.e. measurable. -/
lemma aemeasurable_of_mem_interior_integrableExpSet (hv : v âˆˆ interior (integrableExpSet X Î¼)) :
    AEMeasurable X Î¼ := by
  rw [mem_interior_iff_mem_nhds, mem_nhds_iff_exists_Ioo_subset] at hv
  obtain âŸ¨l, u, hvlu, h_subsetâŸ© := hv
  let t := ((v - l) âŠ“ (u - v)) / 2
  have h_pos : 0 < (v - l) âŠ“ (u - v) := by simp [hvlu.1, hvlu.2]
  have ht : 0 < t := half_pos h_pos
  by_cases hvt : v + t = 0
  Â· have hvt' : v - t â‰  0 := by
      rw [sub_ne_zero]
      refine fun h_eq â†¦ ht.ne' ?_
      simpa [h_eq] using hvt
    exact aemeasurable_of_aemeasurable_exp_mul hvt'
      (h_subset (sub_half_inf_sub_mem_Ioo hvlu)).aemeasurable
  Â· exact aemeasurable_of_aemeasurable_exp_mul hvt
      (h_subset (add_half_inf_sub_mem_Ioo hvlu)).aemeasurable

/-- If `v` belongs to the interior of the interval `integrableExpSet X Î¼`,
then `|X| ^ p * exp (v * X)` is integrable for all nonnegative `p : â„`. -/
lemma integrable_rpow_abs_mul_exp_of_mem_interior_integrableExpSet
    (hv : v âˆˆ interior (integrableExpSet X Î¼)) {p : â„} (hp : 0 â‰¤ p) :
    Integrable (fun Ï‰ â†¦ |X Ï‰| ^ p * exp (v * X Ï‰)) Î¼ := by
  rw [mem_interior_iff_mem_nhds, mem_nhds_iff_exists_Ioo_subset] at hv
  obtain âŸ¨l, u, hvlu, h_subsetâŸ© := hv
  have h_pos : 0 < (v - l) âŠ“ (u - v) := by simp [hvlu.1, hvlu.2]
  refine integrable_rpow_abs_mul_exp_of_integrable_exp_mul
    (t := min (v - l) (u - v) / 2) ?_ ?_ ?_ hp
  Â· positivity
  Â· exact h_subset (add_half_inf_sub_mem_Ioo hvlu)
  Â· exact h_subset (sub_half_inf_sub_mem_Ioo hvlu)

/-- If `v` belongs to the interior of the interval `integrableExpSet X Î¼`,
then `|X| ^ n * exp (v * X)` is integrable for all `n : â„•`. -/
lemma integrable_pow_abs_mul_exp_of_mem_interior_integrableExpSet
    (hv : v âˆˆ interior (integrableExpSet X Î¼)) (n : â„•) :
    Integrable (fun Ï‰ â†¦ |X Ï‰| ^ n * exp (v * X Ï‰)) Î¼ := by
  convert integrable_rpow_abs_mul_exp_of_mem_interior_integrableExpSet hv
    (by positivity : 0 â‰¤ (n : â„)) with Ï‰
  simp

/-- If `v` belongs to the interior of the interval `integrableExpSet X Î¼`,
then `X ^ p * exp (v * X)` is integrable for all nonnegative `p : â„`. -/
lemma integrable_rpow_mul_exp_of_mem_interior_integrableExpSet
    (hv : v âˆˆ interior (integrableExpSet X Î¼)) {p : â„} (hp : 0 â‰¤ p) :
    Integrable (fun Ï‰ â†¦ X Ï‰ ^ p * exp (v * X Ï‰)) Î¼ := by
  rw [mem_interior_iff_mem_nhds, mem_nhds_iff_exists_Ioo_subset] at hv
  obtain âŸ¨l, u, hvlu, h_subsetâŸ© := hv
  have h_pos : 0 < (v - l) âŠ“ (u - v) := by simp [hvlu.1, hvlu.2]
  refine integrable_rpow_mul_exp_of_integrable_exp_mul
    (t := min (v - l) (u - v) / 2) ?_ ?_ ?_ hp
  Â· positivity
  Â· exact h_subset (add_half_inf_sub_mem_Ioo hvlu)
  Â· exact h_subset (sub_half_inf_sub_mem_Ioo hvlu)

/-- If `v` belongs to the interior of the interval `integrableExpSet X Î¼`,
then `X ^ n * exp (v * X)` is integrable for all `n : â„•`. -/
lemma integrable_pow_mul_exp_of_mem_interior_integrableExpSet
    (hv : v âˆˆ interior (integrableExpSet X Î¼)) (n : â„•) :
    Integrable (fun Ï‰ â†¦ X Ï‰ ^ n * exp (v * X Ï‰)) Î¼ := by
  convert integrable_rpow_mul_exp_of_mem_interior_integrableExpSet hv
    (by positivity : 0 â‰¤ (n : â„)) with Ï‰
  simp

/-- If 0 belongs to the interior of the interval `integrableExpSet X Î¼`,
then `|X| ^ n` is integrable for all nonnegative `p : â„`. -/
lemma integrable_rpow_abs_of_mem_interior_integrableExpSet
    (h : 0 âˆˆ interior (integrableExpSet X Î¼)) {p : â„} (hp : 0 â‰¤ p) :
    Integrable (fun Ï‰ â†¦ |X Ï‰| ^ p) Î¼ := by
  convert integrable_rpow_abs_mul_exp_of_mem_interior_integrableExpSet h hp using 1
  simp

/-- If 0 belongs to the interior of the interval `integrableExpSet X Î¼`,
then `|X| ^ n` is integrable for all `n : â„•`. -/
lemma integrable_pow_abs_of_mem_interior_integrableExpSet
    (h : 0 âˆˆ interior (integrableExpSet X Î¼)) (n : â„•) :
    Integrable (fun Ï‰ â†¦ |X Ï‰| ^ n) Î¼ := by
  convert integrable_pow_abs_mul_exp_of_mem_interior_integrableExpSet h n
  simp

/-- If 0 belongs to the interior of the interval `integrableExpSet X Î¼`,
then `X ^ n` is integrable for all nonnegative `p : â„`. -/
lemma integrable_rpow_of_mem_interior_integrableExpSet
    (h : 0 âˆˆ interior (integrableExpSet X Î¼)) {p : â„} (hp : 0 â‰¤ p) :
    Integrable (fun Ï‰ â†¦ X Ï‰ ^ p) Î¼ := by
  convert integrable_rpow_mul_exp_of_mem_interior_integrableExpSet h hp using 1
  simp

/-- If 0 belongs to the interior of the interval `integrableExpSet X Î¼`,
then `X ^ n` is integrable for all `n : â„•`. -/
lemma integrable_pow_of_mem_interior_integrableExpSet
    (h : 0 âˆˆ interior (integrableExpSet X Î¼)) (n : â„•) :
    Integrable (fun Ï‰ â†¦ X Ï‰ ^ n) Î¼ := by
  convert integrable_pow_mul_exp_of_mem_interior_integrableExpSet h n
  simp

/-- If 0 belongs to the interior of `integrableExpSet X Î¼`, then `X` is in `â„’p` for all
finite `p`. -/
lemma memLp_of_mem_interior_integrableExpSet (h : 0 âˆˆ interior (integrableExpSet X Î¼)) (p : â„â‰¥0) :
    MemLp X p Î¼ := by
  have hX : AEMeasurable X Î¼ := aemeasurable_of_mem_interior_integrableExpSet h
  by_cases hp_zero : p = 0
  Â· simp only [hp_zero, ENNReal.coe_zero, memLp_zero_iff_aestronglyMeasurable]
    exact hX.aestronglyMeasurable
  rw [â† integrable_norm_rpow_iff hX.aestronglyMeasurable (mod_cast hp_zero) (by simp)]
  simp only [norm_eq_abs, ENNReal.coe_toReal]
  exact integrable_rpow_abs_of_mem_interior_integrableExpSet h p.2

@[deprecated (since := "2025-02-21")]
alias memğ“›p_of_mem_interior_integrableExpSet := memLp_of_mem_interior_integrableExpSet

section Complex

open Complex

variable {z : â„‚}

lemma integrable_cexp_mul_of_re_mem_integrableExpSet (hX : AEMeasurable X Î¼)
    (hz : z.re âˆˆ integrableExpSet X Î¼) :
    Integrable (fun Ï‰ â†¦ cexp (z * X Ï‰)) Î¼ := by
  rw [â† integrable_norm_iff]
  Â· simpa [Complex.norm_eq_abs, Complex.abs_exp] using hz
  Â· exact AEMeasurable.aestronglyMeasurable (by fun_prop)

lemma integrable_cexp_mul_of_re_mem_interior_integrableExpSet
    (hz : z.re âˆˆ interior (integrableExpSet X Î¼)) :
    Integrable (fun Ï‰ â†¦ cexp (z * X Ï‰)) Î¼ :=
  integrable_cexp_mul_of_re_mem_integrableExpSet
    (aemeasurable_of_mem_interior_integrableExpSet hz) (interior_subset hz)

lemma integrable_rpow_abs_mul_cexp_of_re_mem_interior_integrableExpSet
    (hz : z.re âˆˆ interior (integrableExpSet X Î¼)) {p : â„} (hp : 0 â‰¤ p) :
    Integrable (fun Ï‰ â†¦ (|X Ï‰| ^ p : â„) * cexp (z * X Ï‰)) Î¼ := by
  have hX : AEMeasurable X Î¼ := aemeasurable_of_mem_interior_integrableExpSet hz
  rw [â† integrable_norm_iff]
  swap; Â· exact AEMeasurable.aestronglyMeasurable (by fun_prop)
  simpa [abs_rpow_of_nonneg (abs_nonneg _), Complex.abs_exp]
    using integrable_rpow_abs_mul_exp_of_mem_interior_integrableExpSet hz hp

lemma integrable_pow_abs_mul_cexp_of_re_mem_interior_integrableExpSet
    (hz : z.re âˆˆ interior (integrableExpSet X Î¼)) (n : â„•) :
    Integrable (fun Ï‰ â†¦ |X Ï‰| ^ n * cexp (z * X Ï‰)) Î¼ := by
  convert integrable_rpow_abs_mul_cexp_of_re_mem_interior_integrableExpSet hz (Nat.cast_nonneg n)
  simp

lemma integrable_rpow_mul_cexp_of_re_mem_interior_integrableExpSet
    (hz : z.re âˆˆ interior (integrableExpSet X Î¼)) {p : â„} (hp : 0 â‰¤ p) :
    Integrable (fun Ï‰ â†¦ (X Ï‰ ^ p : â„) * cexp (z * X Ï‰)) Î¼ := by
  have hX : AEMeasurable X Î¼ := aemeasurable_of_mem_interior_integrableExpSet hz
  rw [â† integrable_norm_iff]
  swap; Â· exact AEMeasurable.aestronglyMeasurable (by fun_prop)
  simp only [norm_mul, norm_real, Real.norm_eq_abs, Complex.norm_eq_abs, Complex.abs_exp, mul_re,
    ofReal_re, ofReal_im, mul_zero, sub_zero, Complex.abs_ofReal]
  refine (integrable_rpow_abs_mul_exp_of_mem_interior_integrableExpSet hz hp).mono ?_ ?_
  Â· exact AEMeasurable.aestronglyMeasurable (by fun_prop)
  refine ae_of_all _ fun Ï‰ â†¦ ?_
  simp only [norm_mul, Real.norm_eq_abs, Complex.abs_abs, Real.abs_exp]
  gcongr
  exact abs_rpow_le_abs_rpow _ _

lemma integrable_pow_mul_cexp_of_re_mem_interior_integrableExpSet
    (hz : z.re âˆˆ interior (integrableExpSet X Î¼)) (n : â„•) :
    Integrable (fun Ï‰ â†¦ X Ï‰ ^ n * cexp (z * X Ï‰)) Î¼ := by
  convert integrable_rpow_mul_cexp_of_re_mem_interior_integrableExpSet hz (Nat.cast_nonneg n)
  simp

end Complex

end IntegrableExpSet

end FiniteMoments

end ProbabilityTheory
