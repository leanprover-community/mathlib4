/-
Copyright (c) 2022 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import Mathlib.Probability.IdentDistrib

/-!
# Moments and moment-generating function

## Main definitions

* `ProbabilityTheory.moment X p Œº`: `p`th moment of a real random variable `X` with respect to
  measure `Œº`, `Œº[X^p]`
* `ProbabilityTheory.centralMoment X p Œº`:`p`th central moment of `X` with respect to measure `Œº`,
  `Œº[(X - Œº[X])^p]`
* `ProbabilityTheory.mgf X Œº t`: moment-generating function of `X` with respect to measure `Œº`,
  `Œº[exp(t*X)]`
* `ProbabilityTheory.cgf X Œº t`: cumulant-generating function, logarithm of the moment-generating
  function

## Main results

* `ProbabilityTheory.IndepFun.mgf_add`: if two real random variables `X` and `Y` are independent
  and their moment-generating functions are defined at `t`, then
  `mgf (X + Y) Œº t = mgf X Œº t * mgf Y Œº t`
* `ProbabilityTheory.IndepFun.cgf_add`: if two real random variables `X` and `Y` are independent
  and their cumulant-generating functions are defined at `t`, then
  `cgf (X + Y) Œº t = cgf X Œº t + cgf Y Œº t`
* `ProbabilityTheory.measure_ge_le_exp_cgf` and `ProbabilityTheory.measure_le_le_exp_cgf`:
  Chernoff bound on the upper (resp. lower) tail of a random variable. For `t` nonnegative such that
  the cumulant-generating function exists, `‚Ñô(Œµ ‚â§ X) ‚â§ exp(- t*Œµ + cgf X ‚Ñô t)`. See also
  `ProbabilityTheory.measure_ge_le_exp_mul_mgf` and
  `ProbabilityTheory.measure_le_le_exp_mul_mgf` for versions of these results using `mgf` instead
  of `cgf`.
-/


open MeasureTheory Filter Finset Real

noncomputable section

open scoped MeasureTheory ProbabilityTheory ENNReal NNReal

namespace ProbabilityTheory

variable {Œ© Œπ : Type*} {m : MeasurableSpace Œ©} {X : Œ© ‚Üí ‚Ñù} {p : ‚Ñï} {Œº : Measure Œ©}

/-- Moment of a real random variable, `Œº[X ^ p]`. -/
def moment (X : Œ© ‚Üí ‚Ñù) (p : ‚Ñï) (Œº : Measure Œ©) : ‚Ñù :=
  Œº[X ^ p]

lemma moment_def (X : Œ© ‚Üí ‚Ñù) (p : ‚Ñï) (Œº : Measure Œ©) :
    moment X p Œº = Œº[X ^ p] := rfl

/-- Central moment of a real random variable, `Œº[(X - Œº[X]) ^ p]`. -/
def centralMoment (X : Œ© ‚Üí ‚Ñù) (p : ‚Ñï) (Œº : Measure Œ©) : ‚Ñù :=
  Œº[(X - fun (_ : Œ©) => Œº[X]) ^ p]

@[simp]
theorem moment_zero (hp : p ‚â† 0) : moment 0 p Œº = 0 := by
  simp only [moment, hp, zero_pow, Ne, not_false_iff, Pi.zero_apply, integral_const,
    smul_eq_mul, mul_zero]

@[simp]
lemma moment_zero_measure : moment X p (0 : Measure Œ©) = 0 := by simp [moment]

@[simp]
theorem centralMoment_zero (hp : p ‚â† 0) : centralMoment 0 p Œº = 0 := by
  simp only [centralMoment, hp, Pi.zero_apply, integral_const, smul_eq_mul,
    mul_zero, zero_sub, Pi.pow_apply, Pi.neg_apply, neg_zero, zero_pow, Ne, not_false_iff]

lemma moment_one (X : Œ© ‚Üí ‚Ñù) (Œº : Measure Œ©) :
    moment X 1 Œº = Œº[X] := by simp [moment]

@[simp]
lemma centralMoment_zero_measure : centralMoment X p (0 : Measure Œ©) = 0 := by
  simp [centralMoment]

theorem centralMoment_one' [IsFiniteMeasure Œº] (h_int : Integrable X Œº) :
    centralMoment X 1 Œº = (1 - Œº.real Set.univ) * Œº[X] := by
  simp only [centralMoment, Pi.sub_apply, pow_one]
  rw [integral_sub h_int (integrable_const _)]
  simp only [sub_mul, integral_const, smul_eq_mul, one_mul]

@[simp]
theorem centralMoment_one [IsZeroOrProbabilityMeasure Œº] : centralMoment X 1 Œº = 0 := by
  rcases eq_zero_or_isProbabilityMeasure Œº with rfl | h
  ¬∑ simp [centralMoment]
  by_cases h_int : Integrable X Œº
  ¬∑ rw [centralMoment_one' h_int]
    simp
  ¬∑ simp only [centralMoment, Pi.sub_apply, pow_one]
    have : ¬¨Integrable (fun x => X x - integral Œº X) Œº := by
      refine fun h_sub => h_int ?_
      have h_add : X = (fun x => X x - integral Œº X) + fun _ => integral Œº X := by ext1 x; simp
      rw [h_add]
      fun_prop
    rw [integral_undef this]

lemma centralMoment_two_eq_variance (hX : AEMeasurable X Œº) : centralMoment X 2 Œº = variance X Œº :=
  (variance_eq_integral hX).symm

section MomentGeneratingFunction

variable {t : ‚Ñù}

/-- Moment-generating function of a real random variable `X`: `fun t => Œº[exp(t*X)]`. -/
def mgf (X : Œ© ‚Üí ‚Ñù) (Œº : Measure Œ©) (t : ‚Ñù) : ‚Ñù :=
  Œº[fun œâ => exp (t * X œâ)]

/-- Cumulant-generating function of a real random variable `X`: `fun t => log Œº[exp(t*X)]`. -/
def cgf (X : Œ© ‚Üí ‚Ñù) (Œº : Measure Œ©) (t : ‚Ñù) : ‚Ñù :=
  log (mgf X Œº t)

@[simp]
theorem mgf_zero_fun : mgf 0 Œº t = Œº.real Set.univ := by
  simp only [mgf, Pi.zero_apply, mul_zero, exp_zero, integral_const, smul_eq_mul, mul_one]

@[simp]
theorem cgf_zero_fun : cgf 0 Œº t = log (Œº.real Set.univ) := by simp only [cgf, mgf_zero_fun]

@[simp]
theorem mgf_zero_measure : mgf X (0 : Measure Œ©) = 0 := by ext; simp [mgf]

@[simp]
theorem cgf_zero_measure : cgf X (0 : Measure Œ©) = 0 := by ext; simp [cgf]

@[simp]
theorem mgf_const' (c : ‚Ñù) : mgf (fun _ => c) Œº t = Œº.real Set.univ * exp (t * c) := by
  simp only [mgf, integral_const, smul_eq_mul]

theorem mgf_const (c : ‚Ñù) [IsProbabilityMeasure Œº] : mgf (fun _ => c) Œº t = exp (t * c) := by
  simp

@[simp]
theorem cgf_const' [IsFiniteMeasure Œº] (hŒº : Œº ‚â† 0) (c : ‚Ñù) :
    cgf (fun _ => c) Œº t = log (Œº.real Set.univ) + t * c := by
  simp only [cgf, mgf_const']
  rw [log_mul _ (exp_pos _).ne']
  ¬∑ rw [log_exp _]
  ¬∑ rw [Ne, measureReal_eq_zero_iff, Measure.measure_univ_eq_zero]
    simp only [hŒº, not_false_iff]

@[simp]
theorem cgf_const [IsProbabilityMeasure Œº] (c : ‚Ñù) : cgf (fun _ => c) Œº t = t * c := by
  simp only [cgf, mgf_const, log_exp]

@[simp]
theorem mgf_zero' : mgf X Œº 0 = Œº.real Set.univ := by
  simp only [mgf, zero_mul, exp_zero, integral_const, smul_eq_mul, mul_one]

theorem mgf_zero [IsProbabilityMeasure Œº] : mgf X Œº 0 = 1 := by
  simp [mgf_zero']

theorem cgf_zero' : cgf X Œº 0 = log (Œº.real Set.univ) := by simp only [cgf, mgf_zero']

@[simp]
theorem cgf_zero [IsZeroOrProbabilityMeasure Œº] : cgf X Œº 0 = 0 := by
  rcases eq_zero_or_isProbabilityMeasure Œº with rfl | h <;> simp [cgf_zero']

theorem mgf_undef (hX : ¬¨Integrable (fun œâ => exp (t * X œâ)) Œº) : mgf X Œº t = 0 := by
  simp only [mgf, integral_undef hX]

theorem cgf_undef (hX : ¬¨Integrable (fun œâ => exp (t * X œâ)) Œº) : cgf X Œº t = 0 := by
  simp only [cgf, mgf_undef hX, log_zero]

theorem mgf_nonneg : 0 ‚â§ mgf X Œº t := by
  unfold mgf; positivity

theorem mgf_pos' (hŒº : Œº ‚â† 0) (h_int_X : Integrable (fun œâ => exp (t * X œâ)) Œº) :
    0 < mgf X Œº t := by
  simp_rw [mgf]
  have : ‚à´ x : Œ©, exp (t * X x) ‚àÇŒº = ‚à´ x : Œ© in Set.univ, exp (t * X x) ‚àÇŒº := by
    simp only [Measure.restrict_univ]
  rw [this, setIntegral_pos_iff_support_of_nonneg_ae _ _]
  ¬∑ have h_eq_univ : (Function.support fun x : Œ© => exp (t * X x)) = Set.univ := by
      ext1 x
      simp only [Function.mem_support, Set.mem_univ, iff_true]
      exact (exp_pos _).ne'
    rw [h_eq_univ, Set.inter_univ _]
    refine Ne.bot_lt ?_
    simp only [hŒº, ENNReal.bot_eq_zero, Ne, Measure.measure_univ_eq_zero, not_false_iff]
  ¬∑ filter_upwards with x
    rw [Pi.zero_apply]
    exact (exp_pos _).le
  ¬∑ rwa [integrableOn_univ]

theorem mgf_pos [IsProbabilityMeasure Œº] (h_int_X : Integrable (fun œâ => exp (t * X œâ)) Œº) :
    0 < mgf X Œº t :=
  mgf_pos' (IsProbabilityMeasure.ne_zero Œº) h_int_X

lemma mgf_pos_iff [hŒº : NeZero Œº] :
    0 < mgf X Œº t ‚Üî Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) Œº := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ mgf_pos' hŒº.out h‚ü©
  contrapose! h with h
  simp [mgf_undef h]

lemma exp_cgf [hŒº : NeZero Œº] (hX : Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) Œº) :
    exp (cgf X Œº t) = mgf X Œº t := by rw [cgf, exp_log (mgf_pos' hŒº.out hX)]

@[deprecated (since := "2025-03-08")]
alias exp_cgf_of_neZero := exp_cgf

lemma mgf_map {Œ©' : Type*} {mŒ©' : MeasurableSpace Œ©'} {Œº : Measure Œ©'} {Y : Œ©' ‚Üí Œ©} {X : Œ© ‚Üí ‚Ñù}
    (hY : AEMeasurable Y Œº) {t : ‚Ñù} (hX : AEStronglyMeasurable (fun œâ ‚Ü¶ exp (t * X œâ)) (Œº.map Y)) :
    mgf X (Œº.map Y) t = mgf (X ‚àò Y) Œº t := by
  simp_rw [mgf, integral_map hY hX, Function.comp_apply]

lemma mgf_id_map (hX : AEMeasurable X Œº) : mgf id (Œº.map X) = mgf X Œº := by
  ext t
  rw [mgf_map hX, Function.id_comp]
  exact (measurable_const_mul _).exp.aestronglyMeasurable

lemma mgf_congr {Y : Œ© ‚Üí ‚Ñù} (h : X =·µê[Œº] Y) : mgf X Œº t = mgf Y Œº t :=
  integral_congr_ae <| by filter_upwards [h] with œâ hœâ using by rw [hœâ]

lemma mgf_congr_identDistrib {Œ©' : Type*} {mŒ©' : MeasurableSpace Œ©'} {Œº' : Measure Œ©'}
    {Y : Œ©' ‚Üí ‚Ñù} (h : IdentDistrib X Y Œº Œº') :
    mgf X Œº = mgf Y Œº' := by
  rw [‚Üê mgf_id_map h.aemeasurable_fst, ‚Üê mgf_id_map h.aemeasurable_snd, h.map_eq]

theorem mgf_neg : mgf (-X) Œº t = mgf X Œº (-t) := by simp_rw [mgf, Pi.neg_apply, mul_neg, neg_mul]

theorem cgf_neg : cgf (-X) Œº t = cgf X Œº (-t) := by simp_rw [cgf, mgf_neg]

theorem mgf_smul_left (Œ± : ‚Ñù) : mgf (Œ± ‚Ä¢ X) Œº t = mgf X Œº (Œ± * t) := by
  simp_rw [mgf, Pi.smul_apply, smul_eq_mul, mul_comm Œ± t, mul_assoc]

theorem mgf_const_add (Œ± : ‚Ñù) : mgf (fun œâ => Œ± + X œâ) Œº t = exp (t * Œ±) * mgf X Œº t := by
  rw [mgf, mgf, ‚Üê integral_const_mul]
  congr with x
  dsimp
  rw [mul_add, exp_add]

theorem mgf_add_const (Œ± : ‚Ñù) : mgf (fun œâ => X œâ + Œ±) Œº t = mgf X Œº t * exp (t * Œ±) := by
  simp only [add_comm, mgf_const_add, mul_comm]

lemma mgf_add_measure {ŒΩ : Measure Œ©}
    (hŒº : Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) Œº) (hŒΩ : Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) ŒΩ) :
    mgf X (Œº + ŒΩ) t = mgf X Œº t + mgf X ŒΩ t := by
  rw [mgf, integral_add_measure hŒº hŒΩ, mgf, mgf]

lemma mgf_sum_measure {Œπ : Type*} {Œº : Œπ ‚Üí Measure Œ©}
    (hŒº : Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) (Measure.sum Œº)) :
    mgf X (Measure.sum Œº) t = ‚àë' i, mgf X (Œº i) t := by
  simp_rw [mgf, integral_sum_measure hŒº]

lemma mgf_smul_measure (c : ‚Ñù‚â•0‚àû) : mgf X (c ‚Ä¢ Œº) t = c.toReal * mgf X Œº t := by
  rw [mgf, integral_smul_measure, mgf, smul_eq_mul]

/-- The moment-generating function is monotone in the random variable for `t ‚â• 0`. -/
lemma mgf_mono_of_nonneg {Y : Œ© ‚Üí ‚Ñù} (hXY : X ‚â§·µê[Œº] Y) (ht : 0 ‚â§ t)
    (htY : Integrable (fun œâ ‚Ü¶ exp (t * Y œâ)) Œº) :
    mgf X Œº t ‚â§ mgf Y Œº t := by
  by_cases htX : Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) Œº
  ¬∑ refine integral_mono_ae htX htY ?_
    filter_upwards [hXY] with œâ hœâ using by gcongr
  ¬∑ rw [mgf_undef htX]
    exact mgf_nonneg

/-- The moment-generating function is antitone in the random variable for `t ‚â§ 0`. -/
lemma mgf_anti_of_nonpos {Y : Œ© ‚Üí ‚Ñù} (hXY : X ‚â§·µê[Œº] Y) (ht : t ‚â§ 0)
    (htX : Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) Œº) :
    mgf Y Œº t ‚â§ mgf X Œº t := by
  by_cases htY : Integrable (fun œâ ‚Ü¶ exp (t * Y œâ)) Œº
  ¬∑ refine integral_mono_ae htY htX ?_
    filter_upwards [hXY] with œâ hœâ using exp_monotone <| mul_le_mul_of_nonpos_left hœâ ht
  ¬∑ rw [mgf_undef htY]
    exact mgf_nonneg

section IndepFun

/-- This is a trivial application of `IndepFun.comp` but it will come up frequently. -/
theorem IndepFun.exp_mul {X Y : Œ© ‚Üí ‚Ñù} (h_indep : X ‚üÇ·µ¢[Œº] Y) (s t : ‚Ñù) :
    (fun œâ => exp (s * X œâ)) ‚üÇ·µ¢[Œº] (fun œâ => exp (t * Y œâ)) := by
  have h_meas : ‚àÄ t, Measurable fun x => exp (t * x) := fun t => (measurable_id'.const_mul t).exp
  change IndepFun ((fun x => exp (s * x)) ‚àò X) ((fun x => exp (t * x)) ‚àò Y) Œº
  exact IndepFun.comp h_indep (h_meas s) (h_meas t)

theorem IndepFun.mgf_add {X Y : Œ© ‚Üí ‚Ñù} (h_indep : X ‚üÇ·µ¢[Œº] Y)
    (hX : AEStronglyMeasurable (fun œâ => exp (t * X œâ)) Œº)
    (hY : AEStronglyMeasurable (fun œâ => exp (t * Y œâ)) Œº) :
    mgf (X + Y) Œº t = mgf X Œº t * mgf Y Œº t := by
  simp_rw [mgf, Pi.add_apply, mul_add, exp_add]
  exact (h_indep.exp_mul t t).integral_mul_eq_mul_integral hX hY

theorem IndepFun.mgf_add' {X Y : Œ© ‚Üí ‚Ñù} (h_indep : X ‚üÇ·µ¢[Œº] Y) (hX : AEStronglyMeasurable X Œº)
    (hY : AEStronglyMeasurable Y Œº) : mgf (X + Y) Œº t = mgf X Œº t * mgf Y Œº t := by
  have A : Continuous fun x : ‚Ñù => exp (t * x) := by fun_prop
  have h'X : AEStronglyMeasurable (fun œâ => exp (t * X œâ)) Œº :=
    A.aestronglyMeasurable.comp_aemeasurable hX.aemeasurable
  have h'Y : AEStronglyMeasurable (fun œâ => exp (t * Y œâ)) Œº :=
    A.aestronglyMeasurable.comp_aemeasurable hY.aemeasurable
  exact h_indep.mgf_add h'X h'Y

theorem IndepFun.cgf_add {X Y : Œ© ‚Üí ‚Ñù} (h_indep : X ‚üÇ·µ¢[Œº] Y)
    (h_int_X : Integrable (fun œâ => exp (t * X œâ)) Œº)
    (h_int_Y : Integrable (fun œâ => exp (t * Y œâ)) Œº) :
    cgf (X + Y) Œº t = cgf X Œº t + cgf Y Œº t := by
  by_cases hŒº : Œº = 0
  ¬∑ simp [hŒº]
  simp only [cgf, h_indep.mgf_add h_int_X.aestronglyMeasurable h_int_Y.aestronglyMeasurable]
  exact log_mul (mgf_pos' hŒº h_int_X).ne' (mgf_pos' hŒº h_int_Y).ne'

theorem aestronglyMeasurable_exp_mul_add {X Y : Œ© ‚Üí ‚Ñù}
    (h_int_X : AEStronglyMeasurable (fun œâ => exp (t * X œâ)) Œº)
    (h_int_Y : AEStronglyMeasurable (fun œâ => exp (t * Y œâ)) Œº) :
    AEStronglyMeasurable (fun œâ => exp (t * (X + Y) œâ)) Œº := by
  simp_rw [Pi.add_apply, mul_add, exp_add]
  exact AEStronglyMeasurable.mul h_int_X h_int_Y

theorem aestronglyMeasurable_exp_mul_sum {X : Œπ ‚Üí Œ© ‚Üí ‚Ñù} {s : Finset Œπ}
    (h_int : ‚àÄ i ‚àà s, AEStronglyMeasurable (fun œâ => exp (t * X i œâ)) Œº) :
    AEStronglyMeasurable (fun œâ => exp (t * (‚àë i ‚àà s, X i) œâ)) Œº := by
  classical
  induction s using Finset.induction_on with
  | empty =>
    simp only [sum_apply, sum_empty, mul_zero, exp_zero]
    exact aestronglyMeasurable_const
  | insert i s hi_notin_s h_rec =>
    have : ‚àÄ i : Œπ, i ‚àà s ‚Üí AEStronglyMeasurable (fun œâ : Œ© => exp (t * X i œâ)) Œº := fun i hi =>
      h_int i (mem_insert_of_mem hi)
    specialize h_rec this
    rw [sum_insert hi_notin_s]
    apply aestronglyMeasurable_exp_mul_add (h_int i (mem_insert_self _ _)) h_rec

theorem IndepFun.integrable_exp_mul_add {X Y : Œ© ‚Üí ‚Ñù} (h_indep : X ‚üÇ·µ¢[Œº] Y)
    (h_int_X : Integrable (fun œâ => exp (t * X œâ)) Œº)
    (h_int_Y : Integrable (fun œâ => exp (t * Y œâ)) Œº) :
    Integrable (fun œâ => exp (t * (X + Y) œâ)) Œº := by
  simp_rw [Pi.add_apply, mul_add, exp_add]
  exact (h_indep.exp_mul t t).integrable_mul h_int_X h_int_Y

theorem iIndepFun.integrable_exp_mul_sum [IsFiniteMeasure Œº] {X : Œπ ‚Üí Œ© ‚Üí ‚Ñù}
    (h_indep : iIndepFun X Œº) (h_meas : ‚àÄ i, Measurable (X i))
    {s : Finset Œπ} (h_int : ‚àÄ i ‚àà s, Integrable (fun œâ => exp (t * X i œâ)) Œº) :
    Integrable (fun œâ => exp (t * (‚àë i ‚àà s, X i) œâ)) Œº := by
  classical
  induction s using Finset.induction_on with
  | empty =>
    simp only [sum_apply, sum_empty, mul_zero, exp_zero]
    exact integrable_const _
  | insert i s hi_notin_s h_rec =>
    have : ‚àÄ i : Œπ, i ‚àà s ‚Üí Integrable (fun œâ : Œ© => exp (t * X i œâ)) Œº := fun i hi =>
      h_int i (mem_insert_of_mem hi)
    specialize h_rec this
    rw [sum_insert hi_notin_s]
    refine IndepFun.integrable_exp_mul_add ?_ (h_int i (mem_insert_self _ _)) h_rec
    exact (h_indep.indepFun_finset_sum_of_notMem h_meas hi_notin_s).symm

-- TODO(vilin97): weaken `h_meas` to `AEMeasurable (X i)` or `AEStronglyMeasurable (X i)` throughout
-- https://github.com/leanprover-community/mathlib4/issues/20367
theorem iIndepFun.mgf_sum {X : Œπ ‚Üí Œ© ‚Üí ‚Ñù}
    (h_indep : iIndepFun X Œº) (h_meas : ‚àÄ i, Measurable (X i))
    (s : Finset Œπ) : mgf (‚àë i ‚àà s, X i) Œº t = ‚àè i ‚àà s, mgf (X i) Œº t := by
  have : IsProbabilityMeasure Œº := h_indep.isProbabilityMeasure
  classical
  induction s using Finset.induction_on with
  | empty => simp
  | insert i s hi_notin_s h_rec =>
    have h_int' : ‚àÄ i : Œπ, AEStronglyMeasurable (fun œâ : Œ© => exp (t * X i œâ)) Œº := fun i =>
      ((h_meas i).const_mul t).exp.aestronglyMeasurable
    rw [sum_insert hi_notin_s,
      IndepFun.mgf_add (h_indep.indepFun_finset_sum_of_notMem h_meas hi_notin_s).symm (h_int' i)
        (aestronglyMeasurable_exp_mul_sum fun i _ => h_int' i),
      h_rec, prod_insert hi_notin_s]

theorem iIndepFun.cgf_sum {X : Œπ ‚Üí Œ© ‚Üí ‚Ñù}
    (h_indep : iIndepFun X Œº) (h_meas : ‚àÄ i, Measurable (X i))
    {s : Finset Œπ} (h_int : ‚àÄ i ‚àà s, Integrable (fun œâ => exp (t * X i œâ)) Œº) :
    cgf (‚àë i ‚àà s, X i) Œº t = ‚àë i ‚àà s, cgf (X i) Œº t := by
  have : IsProbabilityMeasure Œº := h_indep.isProbabilityMeasure
  simp_rw [cgf]
  rw [‚Üê log_prod fun j hj => ?_]
  ¬∑ rw [h_indep.mgf_sum h_meas]
  ¬∑ exact (mgf_pos (h_int j hj)).ne'

end IndepFun

theorem mgf_congr_of_identDistrib
    (X : Œ© ‚Üí ‚Ñù) {Œ©' : Type*} {m' : MeasurableSpace Œ©'} {Œº' : Measure Œ©'} (X' : Œ©' ‚Üí ‚Ñù)
    (hident : IdentDistrib X X' Œº Œº') (t : ‚Ñù) :
    mgf X Œº t = mgf X' Œº' t := hident.comp (measurable_const_mul t).exp |>.integral_eq

theorem mgf_sum_of_identDistrib
    {X : Œπ ‚Üí Œ© ‚Üí ‚Ñù}
    {s : Finset Œπ} {j : Œπ}
    (h_meas : ‚àÄ i, Measurable (X i))
    (h_indep : iIndepFun X Œº)
    (hident : ‚àÄ i ‚àà s, ‚àÄ j ‚àà s, IdentDistrib (X i) (X j) Œº Œº)
    (hj : j ‚àà s) (t : ‚Ñù) : mgf (‚àë i ‚àà s, X i) Œº t = mgf (X j) Œº t ^ #s := by
  rw [h_indep.mgf_sum h_meas]
  exact Finset.prod_eq_pow_card fun i hi =>
    mgf_congr_of_identDistrib (X i) (X j) (hident i hi j hj) t

section Chernoff

/-- **Chernoff bound** on the upper tail of a real random variable. -/
theorem measure_ge_le_exp_mul_mgf [IsFiniteMeasure Œº] (Œµ : ‚Ñù) (ht : 0 ‚â§ t)
    (h_int : Integrable (fun œâ => exp (t * X œâ)) Œº) :
    Œº.real {œâ | Œµ ‚â§ X œâ} ‚â§ exp (-t * Œµ) * mgf X Œº t := by
  rcases ht.eq_or_lt with ht_zero_eq | ht_pos
  ¬∑ rw [ht_zero_eq.symm]
    simp only [neg_zero, zero_mul, exp_zero, mgf_zero', one_mul]
    gcongr
    exacts [measure_ne_top _ _, Set.subset_univ _]
  calc
    Œº.real {œâ | Œµ ‚â§ X œâ} = Œº.real {œâ | exp (t * Œµ) ‚â§ exp (t * X œâ)} := by
      congr 1 with œâ
      simp only [Set.mem_setOf_eq, exp_le_exp]
      exact ‚ü®fun h => mul_le_mul_of_nonneg_left h ht_pos.le,
        fun h => le_of_mul_le_mul_left h ht_pos‚ü©
    _ ‚â§ (exp (t * Œµ))‚Åª¬π * Œº[fun œâ => exp (t * X œâ)] := by
      have : exp (t * Œµ) * Œº.real {œâ | exp (t * Œµ) ‚â§ exp (t * X œâ)} ‚â§
          Œº[fun œâ => exp (t * X œâ)] :=
        mul_meas_ge_le_integral_of_nonneg (ae_of_all _ fun x => (exp_pos _).le) h_int _
      rwa [mul_comm (exp (t * Œµ))‚Åª¬π, ‚Üê div_eq_mul_inv, le_div_iff‚ÇÄ' (exp_pos _)]
    _ = exp (-t * Œµ) * mgf X Œº t := by rw [neg_mul, exp_neg]; rfl

/-- **Chernoff bound** on the lower tail of a real random variable. -/
theorem measure_le_le_exp_mul_mgf [IsFiniteMeasure Œº] (Œµ : ‚Ñù) (ht : t ‚â§ 0)
    (h_int : Integrable (fun œâ => exp (t * X œâ)) Œº) :
    Œº.real {œâ | X œâ ‚â§ Œµ} ‚â§ exp (-t * Œµ) * mgf X Œº t := by
  rw [‚Üê neg_neg t, ‚Üê mgf_neg, neg_neg, ‚Üê neg_mul_neg (-t)]
  refine Eq.trans_le ?_ (measure_ge_le_exp_mul_mgf (-Œµ) (neg_nonneg.mpr ht) ?_)
  ¬∑ simp only [Pi.neg_apply, neg_le_neg_iff]
  ¬∑ simp_rw [Pi.neg_apply, neg_mul_neg]
    exact h_int

/-- **Chernoff bound** on the upper tail of a real random variable. -/
theorem measure_ge_le_exp_cgf [IsFiniteMeasure Œº] (Œµ : ‚Ñù) (ht : 0 ‚â§ t)
    (h_int : Integrable (fun œâ => exp (t * X œâ)) Œº) :
    Œº.real {œâ | Œµ ‚â§ X œâ} ‚â§ exp (-t * Œµ + cgf X Œº t) := by
  refine (measure_ge_le_exp_mul_mgf Œµ ht h_int).trans ?_
  rw [exp_add]
  exact mul_le_mul le_rfl (le_exp_log _) mgf_nonneg (exp_pos _).le

/-- **Chernoff bound** on the lower tail of a real random variable. -/
theorem measure_le_le_exp_cgf [IsFiniteMeasure Œº] (Œµ : ‚Ñù) (ht : t ‚â§ 0)
    (h_int : Integrable (fun œâ => exp (t * X œâ)) Œº) :
    Œº.real {œâ | X œâ ‚â§ Œµ} ‚â§ exp (-t * Œµ + cgf X Œº t) := by
  refine (measure_le_le_exp_mul_mgf Œµ ht h_int).trans ?_
  rw [exp_add]
  exact mul_le_mul le_rfl (le_exp_log _) mgf_nonneg (exp_pos _).le

end Chernoff

lemma mgf_dirac {x : ‚Ñù} (hX : Œº.map X = .dirac x) (t : ‚Ñù) : mgf X Œº t = exp (x * t) := by
  have : IsProbabilityMeasure (Œº.map X) := by rw [hX]; infer_instance
  rw [‚Üê mgf_id_map (.of_map_ne_zero <| IsProbabilityMeasure.ne_zero _), mgf, hX, integral_dirac,
    mul_comm, id_def]

lemma mgf_dirac' [MeasurableSingletonClass Œ©] {œâ : Œ©} :
    mgf X (Measure.dirac œâ) t = exp (t * X œâ) := by
  rw [mgf, integral_dirac]

end MomentGeneratingFunction

lemma aemeasurable_exp_mul {X : Œ© ‚Üí ‚Ñù} (t : ‚Ñù) (hX : AEMeasurable X Œº) :
    AEStronglyMeasurable (fun œâ ‚Ü¶ rexp (t * X œâ)) Œº :=
  (measurable_exp.comp_aemeasurable (hX.const_mul t)).aestronglyMeasurable

lemma integrable_exp_mul_of_le [IsFiniteMeasure Œº] {X : Œ© ‚Üí ‚Ñù} (t b : ‚Ñù) (ht : 0 ‚â§ t)
    (hX : AEMeasurable X Œº) (hb : ‚àÄ·µê œâ ‚àÇŒº, X œâ ‚â§ b) :
    Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) Œº := by
  refine .of_mem_Icc 0 (rexp (t * b)) (measurable_exp.comp_aemeasurable (hX.const_mul t)) ?_
  filter_upwards [hb] with œâ hb
  exact ‚ü®by positivity, by gcongr‚ü©

lemma integrable_exp_mul_of_mem_Icc [IsFiniteMeasure Œº] {X : Œ© ‚Üí ‚Ñù} {a b t : ‚Ñù}
    (hm : AEMeasurable X Œº) (hb : ‚àÄ·µê œâ ‚àÇŒº, X œâ ‚àà Set.Icc a b) :
    Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) Œº := by
  apply Integrable.of_mem_Icc (exp (min (a * t) (b * t))) (exp (max (a * t) (b * t)))
  ¬∑ exact (measurable_exp.comp_aemeasurable (hm.const_mul t))
  filter_upwards [hb] with œâ ‚ü®hl, hr‚ü©
  simp only [Set.mem_Icc, exp_le_exp, inf_le_iff, le_sup_iff]
  by_cases ht : 0 ‚â§ t
  ¬∑ exact ‚ü®Or.inl (by nlinarith), Or.inr (by nlinarith)‚ü©
  ¬∑ exact ‚ü®Or.inr (by nlinarith), Or.inl (by nlinarith)‚ü©

end ProbabilityTheory

namespace ContinuousLinearMap

variable {ùïú E F : Type*} [RCLike ùïú] [NormedAddCommGroup E] [NormedAddCommGroup F]
    [NormedSpace ùïú E] [NormedSpace ‚Ñù E] [NormedSpace ùïú F] [NormedSpace ‚Ñù F] [CompleteSpace E]
    [CompleteSpace F] [MeasurableSpace E] {Œº : Measure E}

lemma integral_comp_id_comm' (h : Integrable id Œº) (L : E ‚ÜíL[ùïú] F) :
    Œº[L] = L Œº[id] := by
  change ‚à´ x, L (id x) ‚àÇŒº = _
  rw [L.integral_comp_comm h]

lemma integral_comp_id_comm (h : Integrable id Œº) (L : E ‚ÜíL[ùïú] F) :
    Œº[L] = L (‚à´ x, x ‚àÇŒº) :=
  L.integral_comp_id_comm' h

variable [OpensMeasurableSpace E] [MeasurableSpace F] [BorelSpace F] [SecondCountableTopology F]

lemma integral_id_map (h : Integrable id Œº) (L : E ‚ÜíL[ùïú] F) :
    ‚à´ x, x ‚àÇ(Œº.map L) = L (‚à´ x, x ‚àÇŒº) := by
  rw [integral_map (by fun_prop) (by fun_prop)]
  simp [L.integral_comp_id_comm h]

end ContinuousLinearMap
