/-
Copyright (c) 2025 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
module

public import Mathlib.MeasureTheory.Function.ConvergenceInDistribution
public import Mathlib.MeasureTheory.Measure.ProbabilityMeasure
public import Mathlib.Probability.Distributions.Gaussian.CameronMartin
public import Mathlib.Probability.HasLaw

/-!
# Cameron-Martin Theorem

## Main definitions

* `FooBar`

## Main statements

* `fooBar_unique`

## Notation



## Implementation details



## References

* [F. Bar, *Quuxes*][bibkey]

-/

@[expose] public section

open MeasureTheory Filter Complex
open scoped ENNReal NNReal Topology InnerProductSpace

namespace MeasureTheory

variable {Œ± Œπ E : Type*} {m : MeasurableSpace Œ±}
    [NormedAddCommGroup E] [MeasurableSpace E] [BorelSpace E] [SecondCountableTopology E]
    {Œº : Measure Œ±} [IsProbabilityMeasure Œº]
    {f f' : Œπ ‚Üí Œ± ‚Üí E} {g : Œ± ‚Üí E} {l : Filter Œπ}

end MeasureTheory

namespace ProbabilityTheory

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [MeasurableSpace E] [BorelSpace E]
  [CompleteSpace E] [SecondCountableTopology E]
  {Œº : Measure E} [IsGaussian Œº]

/-- An element `x` of the Cameron-Martin space associated to a Gaussian measure has a centered
Gaussian law with variance `‚Äñx‚Äñ‚Çä ^ 2`. -/
lemma hasLaw_cameronMartin (x : cameronMartin Œº) : HasLaw x (gaussianReal 0 (‚Äñx‚Äñ‚Çä ^ 2)) Œº where
  map_eq := by
    by_cases hx0 : x = 0
    ¬∑ simp only [hx0, ZeroMemClass.coe_zero, nnnorm_zero, ne_eq, OfNat.ofNat_ne_zero,
        not_false_eq_true, zero_pow, gaussianReal_zero_var]
      suffices Œº.map (fun _ ‚Ü¶ (0 : ‚Ñù)) = Measure.dirac 0 by convert this; simp
      simp
    have hx_norm_pos : 0 < ‚Äñx‚Äñ := by simp [norm_pos_iff, hx0]
    unfold cameronMartin at x
    have h := x.2
    rw [Submodule.mem_topologicalClosure_iff, mem_closure_iff_seq_limit] at h
    obtain ‚ü®L, hL_mem, hL_tendsto‚ü© := h
    simp only [SetLike.mem_coe, LinearMap.mem_range] at hL_mem
    have hL_ne_zero : ‚àÄ·∂† n in atTop, L n ‚â† 0 := hL_tendsto.eventually_ne (by simp [hx0])
    let L' := fun n ‚Ü¶ (‚Äñx‚Äñ / ‚ÄñL n‚Äñ) ‚Ä¢ L n
    have hL'_mem n : ‚àÉ y, StrongDual.centeredToLp Œº 2 y = L' n := by
      choose y' hy' using hL_mem
      refine ‚ü®(‚Äñx‚Äñ / ‚ÄñL n‚Äñ) ‚Ä¢ y' n, ?_‚ü©
      simp [hy' n, L']
    have hL'_tendsto : Tendsto L' atTop (ùìù x) := by
      unfold L'
      have h_norm : Tendsto (fun n ‚Ü¶ ‚ÄñL n‚Äñ) atTop (ùìù ‚Äñx‚Äñ) := hL_tendsto.norm
      suffices Tendsto (fun n ‚Ü¶ (‚Äñx‚Äñ / ‚ÄñL n‚Äñ) ‚Ä¢ L n) atTop (ùìù ((‚Äñx‚Äñ / ‚Äñx‚Äñ) ‚Ä¢ x)) by
        rwa [div_self hx_norm_pos.ne', one_smul] at this
      refine Tendsto.smul ?_ hL_tendsto
      exact Tendsto.div tendsto_const_nhds h_norm hx_norm_pos.ne'
    choose y hy using hL'_mem
    have hy_map (n : ‚Ñï) (hn : L n ‚â† 0) : Œº.map (y n) = gaussianReal (Œº[y n]) (‚Äñx‚Äñ‚Çä ^ 2) := by
      rw [IsGaussian.map_eq_gaussianReal]
      congr
      rw [‚Üê StrongDual.sq_norm_centeredToLp_two IsGaussian.memLp_two_id, hy n]
      unfold L'
      simp only [AddSubgroupClass.coe_norm, norm_smul, norm_div, norm_norm]
      rw [div_mul_cancel‚ÇÄ]
      ¬∑ norm_cast
        rw [Real.toNNReal_pow (norm_nonneg _), norm_toNNReal]
      ¬∑ simp [hn]
    have hL'_map n (hn : L n ‚â† 0) : Œº.map (L' n) = gaussianReal 0 (‚Äñx‚Äñ‚Çä ^ 2) := by
      have h_eq : L' n =·µê[Œº] fun x ‚Ü¶ y n x - Œº[y n] := by
        rw [‚Üê hy]
        filter_upwards [StrongDual.centeredToLp_apply (Œº := Œº) memLp_two_id (y n)] with z hz
        simp only [hz, map_sub, sub_right_inj]
        rw [IsGaussian.integral_dual]
      rw [Measure.map_congr h_eq]
      simpa using gaussianReal_sub_const' (hy_map n hn) (Œº[y n])
    have hL'_prob n : IsProbabilityMeasure (Œº.map (L' n)) :=
      Measure.isProbabilityMeasure_map (by fun_prop)
    let ŒΩ n : ProbabilityMeasure ‚Ñù := ‚ü®Œº.map (L' n), hL'_prob n‚ü©
    have h_eventuallyEq : ‚àÄ·∂† n in atTop, ŒΩ n = ‚ü®gaussianReal 0 (‚Äñx‚Äñ‚Çä ^ 2), inferInstance‚ü© := by
      filter_upwards [hL_ne_zero] with n hn
      unfold ŒΩ
      simp_rw [hL'_map n hn]
    have hŒΩ_tendsto_1 : Tendsto ŒΩ atTop (ùìù ‚ü®gaussianReal 0 (‚Äñx‚Äñ‚Çä ^ 2), inferInstance‚ü©) := by
      rw [tendsto_congr' h_eventuallyEq]
      exact tendsto_const_nhds
    have hx_prob : IsProbabilityMeasure (Œº.map (x : E ‚Üí ‚Ñù)) :=
      Measure.isProbabilityMeasure_map (by fun_prop)
    have hŒΩ_tendsto_2 : Tendsto ŒΩ atTop (ùìù ‚ü®Œº.map x, hx_prob‚ü©) :=
      ((tendstoInMeasure_of_tendsto_Lp hL'_tendsto).tendstoInDistribution
        (fun _ ‚Ü¶ by fun_prop)).tendsto
    have h_eq := tendsto_nhds_unique hŒΩ_tendsto_2 hŒΩ_tendsto_1
    rwa [Subtype.ext_iff] at h_eq

/-- The variance of an element of the Cameron-Martin space is the square of its norm. -/
lemma variance_cameronMartin (x : cameronMartin Œº) :
    Var[x; Œº] = ‚Äñx‚Äñ‚Çä ^ 2 := by
  have : Var[fun y ‚Ü¶ y; Œº.map x] = ‚Äñx‚Äñ‚Çä ^ 2 := by simp [(hasLaw_cameronMartin x).map_eq]
  rwa [variance_map aemeasurable_id' (by fun_prop)] at this

/-- The covariance of two elements of the Cameron-Martin space is their inner product. -/
lemma covariance_cameronMartin (x y : cameronMartin Œº) :
    cov[x, y; Œº] = ‚ü™x, y‚ü´_‚Ñù := by
  rw [covariance_eq_variance_add_sub_div_two (Lp.memLp x.1) (Lp.memLp y.1)]
  have : (x : E ‚Üí ‚Ñù) + (y : E ‚Üí ‚Ñù) =·µê[Œº] (x + y : cameronMartin Œº) := by
    simp only [Submodule.coe_add, AddSubgroup.coe_add]
    exact (AEEqFun.coeFn_add _ _).symm
  simp_rw [variance_congr this, variance_cameronMartin,
    real_inner_eq_norm_add_mul_self_sub_norm_mul_self_sub_norm_mul_self_div_two]
  simp [pow_two]

lemma isProbabilityMeasure_withDensity_cameronMartin (x : cameronMartin Œº) :
    IsProbabilityMeasure (Œº.withDensity fun y ‚Ü¶ .ofReal (.exp (x y - ‚Äñx‚Äñ ^ 2 / 2))) where
  measure_univ := by
    rw [withDensity_apply _ .univ, setLIntegral_univ]
    calc ‚à´‚Åª a, .ofReal (.exp (x a - ‚Äñx‚Äñ ^ 2 / 2)) ‚àÇŒº
    _ = .ofReal (.exp (- ‚Äñx‚Äñ ^ 2 / 2)) * ‚à´‚Åª a, .ofReal (.exp (x a)) ‚àÇŒº := by
      simp_rw [sub_eq_add_neg, Real.exp_add, ENNReal.ofReal_mul (Real.exp_nonneg _)]
      rw [lintegral_mul_const _ (by fun_prop), mul_comm, neg_div]
    _ = .ofReal (.exp (- ‚Äñx‚Äñ ^ 2 / 2)) * ‚à´‚Åª a, .ofReal (.exp a) ‚àÇ(Œº.map x) := by
      rw [lintegral_map (by fun_prop) (by fun_prop)]
    _ = .ofReal (.exp (- ‚Äñx‚Äñ ^ 2 / 2)) * ‚à´‚Åª a, .ofReal (.exp a) ‚àÇ(gaussianReal 0 (‚Äñx‚Äñ‚Çä ^ 2)) := by
      rw [(hasLaw_cameronMartin x).map_eq]
    _ = .ofReal (.exp (- ‚Äñx‚Äñ ^ 2 / 2)) * .ofReal (.exp (‚Äñx‚Äñ ^ 2 / 2)) := by
      congr
      have h := mgf_id_gaussianReal (Œº := (0 : ‚Ñù)) (v := ‚Äñx‚Äñ‚Çä ^ 2)
      rw [funext_iff] at h
      specialize h 1
      simp only [mgf, id_eq, one_mul, mul_one, NNReal.coe_pow, coe_nnnorm, one_pow, zero_add] at h
      rw [‚Üê h, ofReal_integral_eq_lintegral_ofReal]
      ¬∑ simpa using (integrable_exp_mul_gaussianReal (Œº := (0 : ‚Ñù)) (v := ‚Äñx‚Äñ‚Çä ^ 2) 1)
      ¬∑ exact ae_of_all _ fun _ ‚Ü¶ Real.exp_nonneg _
    _ = 1 := by
      rw [‚Üê ENNReal.ofReal_mul (Real.exp_nonneg _), ‚Üê Real.exp_add]
      ring_nf
      simp

lemma some_equality_in_Real'' (x : cameronMartin Œº) (L : StrongDual ‚Ñù E) (t : ‚Ñù) :
    ‚à´ u, exp ((L u - t * x u) * I - ‚Äñx‚Äñ ^ 2 / 2) ‚àÇŒº
      = exp (- ‚Äñx‚Äñ ^ 2 / 2) * ‚à´ u, exp ((L - t ‚Ä¢ x) u * I + Œº[L] * I) ‚àÇŒº := by
  calc ‚à´ u, exp ((L u - t * x u) * I - ‚Äñx‚Äñ ^ 2 / 2) ‚àÇŒº
  _ = exp (- ‚Äñx‚Äñ ^ 2 / 2) * ‚à´ u, exp ((L u - t * x u) * I) ‚àÇŒº := by
    simp_rw [sub_eq_add_neg, exp_add]
    rw [integral_mul_const, mul_comm (exp _), neg_div]
  _ = exp (- ‚Äñx‚Äñ ^ 2 / 2) * ‚à´ u, exp ((L u - Œº[L] - t * x u) * I + Œº[L] * I) ‚àÇŒº := by
    congr with u
    congr
    ring
  _ = exp (- ‚Äñx‚Äñ ^ 2 / 2) * ‚à´ u, exp ((L - t ‚Ä¢ x) u * I + Œº[L] * I) ‚àÇŒº := by
    congr 1
    refine integral_congr_ae ?_
    have h_eq : (L : cameronMartin Œº) - t ‚Ä¢ x =·µê[Œº] fun u ‚Ü¶ L u - Œº[L] - t * x u := by
      simp only [cmOfDual_apply, AddSubgroupClass.coe_sub, SetLike.val_smul]
      rw [IsGaussian.integral_dual L]
      filter_upwards [StrongDual.centeredToLp_apply (Œº := Œº) memLp_two_id L,
        AEEqFun.coeFn_sub (Œ≥ := ‚Ñù) (StrongDual.centeredToLp Œº 2 L) (t ‚Ä¢ x),
        Lp.coeFn_smul (E := ‚Ñù) t (x : Lp ‚Ñù 2 Œº)] with u h_toLp h_sub h_smul
      simp only [SetLike.val_smul, Pi.sub_apply] at h_sub
      simp only [Pi.smul_apply, smul_eq_mul] at h_smul
      simp only [map_sub] at h_toLp
      rw [‚Üê h_smul, ‚Üê h_toLp, ‚Üê h_sub]
      rfl
    filter_upwards [h_eq] with u hu
    rw [hu, integral_complex_ofReal]
    simp

lemma some_equality_in_Real' (x : cameronMartin Œº) (L : StrongDual ‚Ñù E) (t : ‚Ñù) :
    ‚à´ u, exp ((L u - t * x u) * I - ‚Äñx‚Äñ ^ 2 / 2) ‚àÇŒº
      = exp (- ‚Äñx‚Äñ ^ 2 / 2 + Œº[L] * I)
        * ‚à´ u : ‚Ñù, exp (u * I) ‚àÇ(gaussianReal 0 (‚ÄñL - t ‚Ä¢ x‚Äñ‚Çä ^ 2)) := by
  calc ‚à´ u, exp ((L u - t * x u) * I - ‚Äñx‚Äñ ^ 2 / 2) ‚àÇŒº
  _ = exp (- ‚Äñx‚Äñ ^ 2 / 2) * ‚à´ u, exp ((L - t ‚Ä¢ x) u * I + Œº[L] * I) ‚àÇŒº :=
    some_equality_in_Real'' x L t
  _ = exp (- ‚Äñx‚Äñ ^ 2 / 2)
      * ‚à´ u : ‚Ñù, exp (u * I + Œº[L] * I) ‚àÇ(Œº.map (((L : cameronMartin Œº) - t ‚Ä¢ x))) := by
    rw [integral_map (by fun_prop) (by fun_prop)]
  _ = exp (- ‚Äñx‚Äñ ^ 2 / 2)
      * ‚à´ u : ‚Ñù, exp (u * I + Œº[L] * I) ‚àÇ(gaussianReal 0 (‚ÄñL - t ‚Ä¢ x‚Äñ‚Çä ^ 2)) := by
    rw [(hasLaw_cameronMartin (((L : cameronMartin Œº) - t ‚Ä¢ x))).map_eq]
  _ = exp (- ‚Äñx‚Äñ ^ 2 / 2 + Œº[L] * I)
      * ‚à´ u : ‚Ñù, exp (u * I) ‚àÇ(gaussianReal 0 (‚ÄñL - t ‚Ä¢ x‚Äñ‚Çä ^ 2)) := by
    rw [exp_add, mul_assoc]
    congr 1
    simp_rw [exp_add]
    rw [integral_mul_const, mul_comm _ (exp _)]

lemma some_equality_in_Real (x : cameronMartin Œº) (L : StrongDual ‚Ñù E) (t : ‚Ñù) :
    ‚à´ u, exp ((L u - t * x u) * I - ‚Äñx‚Äñ ^ 2 / 2) ‚àÇŒº
      = exp (t * L (cmCoe x) - (1 + t ^ 2) / 2 * ‚Äñx‚Äñ ^ 2 + Œº[L] * I - Var[L; Œº] / 2) := by
  calc ‚à´ u, exp ((L u - t * x u) * I - ‚Äñx‚Äñ ^ 2 / 2) ‚àÇŒº
  _ = exp (- ‚Äñx‚Äñ ^ 2 / 2 + Œº[L] * I) * ‚à´ u : ‚Ñù, exp (u * I) ‚àÇ(gaussianReal 0 (‚ÄñL - t ‚Ä¢ x‚Äñ‚Çä ^ 2)) :=
    some_equality_in_Real' x L t
  _ = exp (- ‚Äñx‚Äñ ^ 2 / 2 + Œº[L] * I - ‚ÄñL - t ‚Ä¢ x‚Äñ ^ 2 / 2) := by
    conv_lhs => rw [exp_add]
    conv_rhs => rw [add_sub_assoc, exp_add, sub_eq_add_neg, exp_add, ‚Üê mul_assoc]
    have h := charFun_gaussianReal (Œº := 0) (v := ‚ÄñL - t ‚Ä¢ x‚Äñ‚Çä ^ 2) 1
    simp only [charFun, RCLike.inner_apply, conj_trivial, one_mul, Complex.ofReal_one,
      Complex.ofReal_zero, mul_zero, zero_mul, NNReal.coe_pow, coe_nnnorm, Complex.ofReal_pow,
      one_pow, mul_one, zero_sub] at h
    rw [h]
  _ = exp (t * L (cmCoe x) - (1 + t ^ 2) / 2 * ‚Äñx‚Äñ ^ 2 + Œº[L] * I - Var[L; Œº] / 2) := by
    have h_inner : (t : ‚ÑÇ) * L (cmCoe x) = ‚ü™cmOfDual Œº L, t ‚Ä¢ x‚ü´_‚Ñù := by
      rw [‚Üê apply_cmCoe_eq_inner]
      simp
    rw [h_inner, real_inner_eq_norm_mul_self_add_norm_mul_self_sub_norm_sub_mul_self_div_two]
    simp_rw [‚Üê pow_two]
    rw [sq_norm_cmOfDual (Œº := Œº) L]
    simp only [norm_smul, Real.norm_eq_abs, mul_pow, sq_abs, Complex.ofReal_div, Complex.ofReal_sub,
      Complex.ofReal_add, Complex.ofReal_mul, Complex.ofReal_pow, Complex.ofReal_ofNat]
    ring_nf

lemma hasDerivAt_integral_exp_cameronMartin (x : cameronMartin Œº) (L : StrongDual ‚Ñù E) (z : ‚ÑÇ) :
    HasDerivAt (fun z ‚Ü¶ ‚à´ u, exp ((L u - z * x u) * I) ‚àÇŒº)
      (‚à´ u, - x u * I * exp ((L u - z * x u) * I) ‚àÇŒº) z := by
  refine (hasDerivAt_integral_of_dominated_loc_of_deriv_le
    (bound := fun œâ ‚Ü¶ |x œâ| * Real.exp (z.im * x œâ + |x œâ|))
    (F := fun z œâ ‚Ü¶ cexp ((L œâ - z * x œâ) * I))
    (F' := fun z œâ ‚Ü¶ - x œâ * I * exp ((L œâ - z * x œâ) * I)) zero_lt_one ?_ ?_ (by fun_prop)
      ?_ ?_ ?_).2
  ¬∑ exact .of_forall fun z ‚Ü¶ by fun_prop
  ¬∑ rw [‚Üê integrable_norm_iff (by fun_prop)]
    simp only [norm_exp, mul_re, sub_re, ofReal_re, ofReal_im, mul_zero, sub_zero, I_re, sub_im,
      mul_im, zero_add, zero_sub, I_im, mul_one, sub_neg_eq_add]
    change Integrable ((fun a ‚Ü¶ Real.exp (z.im * a)) ‚àò x) Œº
    rw [‚Üê integrable_map_measure (f := x) (by fun_prop)
      (by fun_prop), (hasLaw_cameronMartin x).map_eq]
    exact integrable_exp_mul_gaussianReal (Œº := 0) (v := ‚Äñx‚Äñ‚Çä ^ 2) z.im
  ¬∑ filter_upwards with œâ Œµ hŒµ
    simp only [neg_mul, norm_neg, norm_mul, norm_real, Real.norm_eq_abs, norm_I, mul_one]
    rw [Complex.norm_exp]
    simp only [mul_re, sub_re, ofReal_re, ofReal_im, mul_zero, sub_zero, I_re, sub_im, mul_im,
      zero_add, zero_sub, I_im, mul_one, sub_neg_eq_add]
    gcongr
    have : Œµ = z + (Œµ - z) := by simp
    rw [this, add_im, add_mul]
    gcongr _ + ?_
    refine (le_abs_self _).trans ?_
    rw [abs_mul]
    conv_rhs => rw [‚Üê one_mul (|x œâ|)]
    gcongr
    refine (abs_im_le_norm _).trans ?_
    simp only [Metric.mem_ball, dist_eq_norm] at hŒµ
    exact hŒµ.le
  ¬∑ change Integrable ((fun œâ ‚Ü¶ |œâ| * Real.exp (z.im * œâ + |œâ|)) ‚àò x) Œº
    rw [‚Üê integrable_map_measure (f := x) (by fun_prop) (by fun_prop),
      (hasLaw_cameronMartin x).map_eq]
    have h := integrable_pow_abs_mul_exp_add_of_integrable_exp_mul (x := 1) (v := z.im) (X := id)
      (t := 2) (Œº := gaussianReal 0 (‚Äñx‚Äñ‚Çä ^ 2)) ?_ ?_ zero_le_one (by simp) 1
    ¬∑ simpa only [id_eq, pow_one, one_mul] using h
    ¬∑ exact integrable_exp_mul_gaussianReal (z.im + 2)
    ¬∑ exact integrable_exp_mul_gaussianReal (z.im - 2)
  ¬∑ filter_upwards with œâ Œµ hŒµ
    simp_rw [sub_mul, sub_eq_add_neg, exp_add, ‚Üê neg_mul, mul_comm (_ * I), mul_assoc]
    refine HasDerivAt.const_mul _ ?_
    simp_rw [neg_mul, mul_comm _ (_ * I), ‚Üê neg_mul]
    simp_rw [‚Üê smul_eq_mul, Complex.exp_eq_exp_‚ÑÇ]
    convert hasDerivAt_exp_smul_const (-x œâ * I : ‚ÑÇ) Œµ using 1
    ¬∑ ext œâ
      congr 1
      simp only [smul_eq_mul, neg_mul, mul_neg, neg_inj]
      ring
    ¬∑ simp only [smul_eq_mul, neg_mul, mul_neg, neg_inj, mul_eq_mul_right_iff, mul_eq_zero,
        ofReal_eq_zero, I_ne_zero, or_false]
      left
      congr 2
      ring

lemma analyticOnNhd_integral_exp_cameronMartin (x : cameronMartin Œº) (L : StrongDual ‚Ñù E) :
    AnalyticOnNhd ‚ÑÇ (fun z ‚Ü¶ ‚à´ u, exp ((L u - z * x u) * I) ‚àÇŒº) Set.univ := by
  refine DifferentiableOn.analyticOnNhd (fun z hz ‚Ü¶ ?_) isOpen_univ
  have h := hasDerivAt_integral_exp_cameronMartin x L z
  rw [hasDerivAt_iff_hasFDerivAt] at h
  exact h.hasFDerivWithinAt.differentiableWithinAt

lemma some_equality_in_Complex (x : cameronMartin Œº) (L : StrongDual ‚Ñù E) (z : ‚ÑÇ) :
    ‚à´ u, exp ((L u - z * x u) * I - ‚Äñx‚Äñ ^ 2 / 2) ‚àÇŒº
      = exp (z * L (cmCoe x) - (1 + z ^ 2) / 2 * ‚Äñx‚Äñ ^ 2 + Œº[L] * I - Var[L; Œº] / 2) := by
  revert z
  refine funext_iff.mp ?_
  rw [‚Üê Set.eqOn_univ]
  refine AnalyticOnNhd.eqOn_of_preconnected_of_frequently_eq (ùïú := ‚ÑÇ) (E := ‚ÑÇ) (z‚ÇÄ := 0) ?_ ?_
    isPreconnected_univ (Set.mem_univ 0) ?_
  ¬∑ simp_rw [sub_eq_add_neg, exp_add, integral_mul_const]
    refine AnalyticOnNhd.mul ?_ analyticOnNhd_const
    simp_rw [‚Üê sub_eq_add_neg]
    exact analyticOnNhd_integral_exp_cameronMartin _ _
  ¬∑ simp_rw [sub_eq_add_neg, exp_add]
    refine AnalyticOnNhd.mul ?_ analyticOnNhd_const
    refine AnalyticOnNhd.mul ?_ analyticOnNhd_const
    refine AnalyticOnNhd.mul ?_ ?_
    ¬∑ exact (analyticOnNhd_id.mul analyticOnNhd_const).cexp
    ¬∑ refine (AnalyticOnNhd.mul ?_ analyticOnNhd_const).neg.cexp
      exact (analyticOnNhd_const.add (analyticOnNhd_id.pow 2)).mul analyticOnNhd_const
  -- todo: extract lemma: frequently around a point in ‚Ñù implies frequently around the point in ‚ÑÇ.
  -- This is also used in ComplexMGF
  have h_real : ‚àÉ·∂† (t : ‚Ñù) in ùìù[‚â†] 0,
      ‚à´ u, exp ((L u - t * x u) * I - ‚Äñx‚Äñ ^ 2 / 2) ‚àÇŒº
        = .exp (t * L (cmCoe x) - (1 + t ^ 2) / 2 * ‚Äñx‚Äñ ^ 2 + Œº[L] * I - Var[L; Œº] / 2) :=
    .of_forall fun y ‚Ü¶ some_equality_in_Real x L y
  rw [frequently_iff_seq_forall] at h_real ‚ä¢
  obtain ‚ü®xs, hx_tendsto, hx_eq‚ü© := h_real
  refine ‚ü®fun n ‚Ü¶ xs n, ?_, fun n ‚Ü¶ ?_‚ü©
  ¬∑ rw [tendsto_nhdsWithin_iff] at hx_tendsto ‚ä¢
    constructor
    ¬∑ rw [‚Üê Complex.ofReal_zero, tendsto_ofReal_iff]
      exact hx_tendsto.1
    ¬∑ simpa using hx_tendsto.2
  ¬∑ simp only [AddSubgroupClass.coe_norm] at hx_eq
    simp [hx_eq]

lemma charFunDual_withDensity_exp_cameronMartin (x : cameronMartin Œº) (L : StrongDual ‚Ñù E) :
    charFunDual (Œº.withDensity fun y ‚Ü¶ .ofReal (.exp (x y - ‚Äñx‚Äñ ^ 2 / 2))) L
      = exp ((Œº[L] + L (cmCoe x)) * I - Var[L; Œº] / 2) := by
  calc charFunDual (Œº.withDensity fun y ‚Ü¶ .ofReal (.exp (x y - ‚Äñx‚Äñ ^ 2 / 2))) L
  _ = ‚à´ u, exp (L u * I + x u - ‚Äñx‚Äñ ^ 2 / 2) ‚àÇŒº := by
    rw [charFunDual_apply, integral_withDensity_eq_integral_toReal_smul (by fun_prop)]
    swap; ¬∑ exact ae_of_all _ (by finiteness)
    congr with u
    rw [ENNReal.toReal_ofReal (Real.exp_nonneg _), add_sub_assoc, exp_add,
      mul_comm (exp _)]
    simp
  _ = exp ((Œº[L] + L (cmCoe x)) * I - Var[L; Œº] / 2) := by
    have h := some_equality_in_Complex x L I
    simp only [I_sq, add_neg_cancel, zero_div, zero_mul, sub_zero] at h
    convert h using 3
    ¬∑ congr
      simp [mul_comm I, sub_mul, mul_assoc]
    ¬∑ ring

/-- Part of the **Cameron-Martin** theorem. The translation of a Gaussian measure `Œº` by
an element `x` of the Cameron-Martin space is absolutely continuous with respect to `Œº`,
with density `y ‚Ü¶ exp (x y - ‚Äñx‚Äñ ^ 2 / 2)`. -/
theorem map_add_cameronMartin_eq_withDensity (x : cameronMartin Œº) :
    Œº.map (fun y ‚Ü¶ y + cmCoe x) = Œº.withDensity (fun y ‚Ü¶ .ofReal (.exp (x y - ‚Äñx‚Äñ ^ 2 / 2))) := by
  have := isProbabilityMeasure_withDensity_cameronMartin x
  refine Measure.ext_of_charFunDual ?_
  ext L
  rw [charFunDual_map_add_const, IsGaussian.charFunDual_eq, ‚Üê exp_add,
    charFunDual_withDensity_exp_cameronMartin x L]
  congr
  ring

/-- Part of the **Cameron-Martin** theorem. The translation of a Gaussian measure `Œº` by
an element `x` of the Cameron-Martin space is absolutely continuous with respect to `Œº`. -/
theorem absolutelyContinuous_map_add_cameronMartin (x : cameronMartin Œº) :
    Œº.map (fun y ‚Ü¶ y + cmCoe x) ‚â™ Œº := by
  rw [map_add_cameronMartin_eq_withDensity x]
  exact withDensity_absolutelyContinuous _ _

-- defined in another PR. We state its properties here with `sorry` proofs, but they are all proved
-- over there.
def tvDist (Œº ŒΩ : Measure E) : ‚Ñù := sorry

lemma tvDist_le_one {Œº ŒΩ : Measure E} : tvDist Œº ŒΩ ‚â§ 1 := by
  sorry

lemma tvDist_eq_one_iff_mutuallySingular {Œº ŒΩ : Measure E} :
    tvDist Œº ŒΩ = 1 ‚Üî Œº ‚üÇ‚Çò ŒΩ := by
  sorry

lemma tvDist_map_le {F : Type*} {mF : MeasurableSpace F} {Œº ŒΩ : Measure E}
    {f : E ‚Üí F} (hf : Measurable f) :
    tvDist (Œº.map f) (ŒΩ.map f) ‚â§ tvDist Œº ŒΩ := by
  sorry

lemma one_sub_exp_le_tvDist_gaussianReal (Œº‚ÇÅ Œº‚ÇÇ : ‚Ñù) :
    1 - Real.exp (- (Œº‚ÇÅ - Œº‚ÇÇ) ^ 2 / 8) ‚â§ tvDist (gaussianReal Œº‚ÇÅ 1) (gaussianReal Œº‚ÇÇ 1) := by
  sorry

lemma tvDist_dirac_of_ne {x y : E} (hxy : x ‚â† y) :
    tvDist (Measure.dirac x) (Measure.dirac y) = 1 := by
  sorry

/-- Part of the **Cameron-Martin** theorem. The translation of a Gaussian measure `Œº` by
an element `y` which is not in the Cameron-Martin space is mutually singular with respect to `Œº`. -/
theorem mutuallySingular_map_add_of_notMem_range_toInitialSpace (y : E)
    (hy : y ‚àâ Set.range (cmCoe (Œº := Œº))) :
    Œº.map (fun z ‚Ü¶ z + y) ‚üÇ‚Çò Œº := by
  rw [‚Üê tvDist_eq_one_iff_mutuallySingular]
  refine le_antisymm tvDist_le_one ?_
  refine le_of_forall_lt fun c hc ‚Ü¶ ?_
  obtain ‚ü®n, hcn‚ü© : ‚àÉ n : ‚Ñï, c < 1 - Real.exp (- n ^ 2 / 8) := by
    simp_rw [lt_sub_iff_add_lt, ‚Üê lt_sub_iff_add_lt']
    suffices Tendsto (fun n : ‚Ñï ‚Ü¶ Real.exp (- n ^ 2 / 8)) atTop (ùìù 0) by
      refine Eventually.exists (f := atTop) ?_
      refine this.eventually_lt_const ?_
      grind
    change Tendsto ((fun x : ‚Ñù ‚Ü¶ Real.exp (- x ^ 2 / 8)) ‚àò (Nat.cast : ‚Ñï ‚Üí ‚Ñù)) atTop (ùìù 0)
    refine Tendsto.comp ?_ <| tendsto_natCast_atTop_atTop (R := ‚Ñù)
    simp [tendsto_div_const_atBot_iff]
  refine hcn.trans_le ?_
  obtain ‚ü®L, hL_var, hL_lt‚ü© : ‚àÉ L : StrongDual ‚Ñù E, (Var[L; Œº] = 1 ‚à® Var[L; Œº] = 0) ‚àß n < L y := by
    simp only [range_cmCoe, Set.mem_setOf_eq, not_exists, not_forall, not_le] at hy
    obtain ‚ü®L, hL_var, hL_lt‚ü© := hy n
    by_cases hL_var_zero : Var[L; Œº] = 0
    ¬∑ exact ‚ü®L, Or.inr hL_var_zero, hL_lt‚ü©
    have h_var_pos : 0 < Var[L; Œº] := by
      refine (variance_nonneg _ _).lt_of_ne' hL_var_zero
    refine ‚ü®‚àöVar[L; Œº]‚Åª¬π ‚Ä¢ L, ?_, ?_‚ü©
    ¬∑ left
      simp only [ContinuousLinearMap.coe_smul']
      rw [variance_smul, Real.sq_sqrt, inv_mul_cancel‚ÇÄ]
      ¬∑ exact h_var_pos.ne'
      ¬∑ simp [variance_nonneg]
    ¬∑ refine hL_lt.trans_le ?_
      simp only [Real.sqrt_inv, ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul]
      conv_lhs => rw [‚Üê one_mul (L y)]
      gcongr
      ¬∑ exact le_trans (by positivity) hL_lt.le
      ¬∑ rw [one_le_inv_iff‚ÇÄ, Real.sqrt_pos, Real.sqrt_le_one]
        simp [hL_var, h_var_pos]
  have h_le : tvDist ((Œº.map (fun z ‚Ü¶ z + y)).map L) (Œº.map L)
      ‚â§ tvDist (Œº.map (fun z ‚Ü¶ z + y)) Œº := tvDist_map_le (by fun_prop)
  refine le_trans ?_ h_le
  simp only [IsGaussian.map_eq_gaussianReal]
  rw [integral_map (by fun_prop) (by fun_prop)]
  simp only [map_add]
  rw [integral_add (by fun_prop) (by fun_prop), variance_map (by fun_prop) (by fun_prop)]
  simp only [integral_const, probReal_univ, smul_eq_mul, one_mul]
  have : L ‚àò (fun z ‚Ü¶ z + y) = fun z ‚Ü¶ L z + L y := by ext; simp
  rw [this, variance_add_const (by fun_prop)]
  by_cases hL_var_zero : Var[L; Œº] = 0
  ¬∑ simp only [hL_var_zero, Real.toNNReal_zero, gaussianReal_zero_var, tsub_le_iff_right,
      ge_iff_le]
    rw [tvDist_dirac_of_ne]
    ¬∑ simp only [le_add_iff_nonneg_right]
      positivity
    ¬∑ simp only [ne_eq, add_eq_left]
      have : (0 : ‚Ñù) ‚â§ n := by positivity
      exact (this.trans_lt hL_lt).ne'
  ¬∑ simp only [hL_var_zero, or_false] at hL_var
    simp only [hL_var, Real.toNNReal_one]
    refine le_trans ?_ (one_sub_exp_le_tvDist_gaussianReal (Œº[L] + L y) Œº[L])
    gcongr
    ring_nf
    exact hL_lt.le

end ProbabilityTheory
