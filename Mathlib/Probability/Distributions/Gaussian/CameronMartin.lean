/-
Copyright (c) 2025 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import Mathlib.Analysis.InnerProductSpace.Completion
import Mathlib.MeasureTheory.Measure.SeparableMeasure
import Mathlib.Probability.Distributions.Gaussian.Fernique
import Mathlib.Probability.Moments.CovarianceBilin

/-!
# Cameron-Martin space

## Main definitions

* `FooBar`

## Main statements

* `fooBar_unique`

## Notation



## Implementation details



## References

* [F. Bar, *Quuxes*][bibkey]

## Tags

Foobars, barfoos
-/

open MeasureTheory NormedSpace UniformSpace
open scoped ENNReal InnerProductSpace

lemma InnerProductSpace.norm_le_dual_bound {E : Type*} [NormedAddCommGroup E]
    [InnerProductSpace ‚Ñù E] [CompleteSpace E]
    (x : E) {M : ‚Ñù} (hMp : 0 ‚â§ M) (hM : ‚àÄ y : E, ‚ü™x, y‚ü´_‚Ñù ‚â§ M * ‚Äñy‚Äñ) :
    ‚Äñx‚Äñ ‚â§ M := by
  refine NormedSpace.norm_le_dual_bound ‚Ñù _ hMp fun f ‚Ü¶ ?_
  let y := (InnerProductSpace.toDual ‚Ñù E).symm f
  obtain hy : f x = ‚ü™x, y‚ü´_‚Ñù := by
    unfold y
    rw [real_inner_comm, InnerProductSpace.toDual_symm_apply]
  rw [hy]
  simp only [Real.norm_eq_abs, abs_le]
  constructor
  ¬∑ specialize hM (-y)
    simp only [inner_neg_right, norm_neg] at hM
    rw [‚Üê neg_le]
    convert hM
    simp [y]
  ¬∑ convert hM y
    simp [y]

lemma InnerProductSpace.norm_le_dual_bound_of_norm_le_one {E : Type*} [NormedAddCommGroup E]
    [InnerProductSpace ‚Ñù E] [CompleteSpace E]
    (x : E) {M : ‚Ñù} (hMp : 0 ‚â§ M) (hM : ‚àÄ y : E, ‚Äñy‚Äñ ‚â§ 1 ‚Üí ‚ü™x, y‚ü´_‚Ñù ‚â§ M) :
    ‚Äñx‚Äñ ‚â§ M := by
  refine InnerProductSpace.norm_le_dual_bound x hMp fun y ‚Ü¶ ?_
  by_cases h_zero : ‚Äñy‚Äñ = 0
  ¬∑ simp only [h_zero, mul_zero]
    rw [inner_eq_zero_of_right _ h_zero]
  specialize hM (‚Äñy‚Äñ‚Åª¬π ‚Ä¢ y) ?_
  ¬∑ simp only [norm_smul, norm_inv, Real.norm_eq_abs, abs_norm]
    exact inv_mul_le_one
  ¬∑ simp only [inner_smul_right] at hM
    rwa [inv_mul_le_iff‚ÇÄ (by positivity), mul_comm] at hM

lemma InnerProductSpace.norm_eq_ciSup_inner {E : Type*} [NormedAddCommGroup E]
    [InnerProductSpace ‚Ñù E] [CompleteSpace E] (x : E) :
    ‚Äñx‚Äñ = ‚®Ü (y : E) (_ : ‚Äñy‚Äñ ‚â§ 1), ‚ü™x, y‚ü´_‚Ñù := by
  have h_ciSup_le y : ‚®Ü (_ : ‚Äñy‚Äñ ‚â§ 1), ‚ü™x, y‚ü´_‚Ñù ‚â§ ‚Äñx‚Äñ := by
    by_cases hy : ‚Äñy‚Äñ ‚â§ 1
    ¬∑ simp only [hy, ciSup_unique]
      calc ‚ü™x, y‚ü´_‚Ñù
      _ ‚â§ ‚Äñx‚Äñ * ‚Äñy‚Äñ := real_inner_le_norm _ _
      _ ‚â§ ‚Äñx‚Äñ * 1 := by gcongr
      _ = ‚Äñx‚Äñ := by rw [mul_one]
    ¬∑ simp [hy]
  have h_bdd : BddAbove (Set.range fun y ‚Ü¶ ‚®Ü (_ : ‚Äñy‚Äñ ‚â§ 1), ‚ü™x, y‚ü´_‚Ñù) := by
    refine ‚ü®‚Äñx‚Äñ, ?_‚ü©
    simp only [mem_upperBounds, Set.mem_range, forall_exists_index, forall_apply_eq_imp_iff]
    intro y
    exact h_ciSup_le y
  refine le_antisymm ?_ (ciSup_le h_ciSup_le)
  ¬∑ refine InnerProductSpace.norm_le_dual_bound_of_norm_le_one x ?_ fun y hy ‚Ü¶ ?_
    ¬∑ exact le_ciSup_of_le h_bdd 0 (by simp)
    ¬∑ exact le_ciSup_of_le h_bdd y (by simp [hy])

namespace UniformSpace.Completion

/-- Extension of a continuous linear map `E ‚ÜíL[R] F` into a complete space to the completion of `E`,
giving a continuous linear map `Completion E ‚ÜíL[R] F`. -/
noncomputable
def continuousLinearMapExtension (R : Type*) {E F : Type*} [Semiring R]
    [UniformSpace E] [AddCommGroup E] [IsUniformAddGroup E]
    [Module R E] [UniformContinuousConstSMul R E]
    [UniformSpace F] [AddCommGroup F] [IsUniformAddGroup F]
    [Module R F] [UniformContinuousConstSMul R F] [T2Space F] [CompleteSpace F]
    (f : E ‚ÜíL[R] F) :
    Completion E ‚ÜíL[R] F where
  toFun x := Completion.extension f x
  map_add' x‚ÇÅ x‚ÇÇ := by
    refine Completion.induction_on‚ÇÇ x‚ÇÅ x‚ÇÇ ?_ fun x‚ÇÅ' x‚ÇÇ' ‚Ü¶ ?_
    ¬∑ have : Continuous (Completion.extension f) := continuous_extension
      exact isClosed_eq (by fun_prop) (by fun_prop)
    ¬∑ rw [extension_coe, extension_coe, ‚Üê map_add, ‚Üê extension_coe (f := f)]
      ¬∑ congr
        norm_cast
      all_goals exact ContinuousLinearMap.uniformContinuous _
  map_smul' r x := by
    simp only [RingHom.id_apply]
    induction x using Completion.induction_on with
    | hp =>
      have h_cont : Continuous (Completion.extension f) := continuous_extension
      refine isClosed_eq ?_ (by fun_prop)
      -- fun_prop fails here (it also fails in the `have` below)
      have : Continuous fun (a : Completion E) ‚Ü¶ r ‚Ä¢ a := continuous_const_smul _
      exact h_cont.comp this
    | ih x =>
      rw [extension_coe, ‚Üê map_smul, ‚Üê extension_coe (f := f)]
      ¬∑ congr
        norm_cast
      all_goals exact ContinuousLinearMap.uniformContinuous _
  cont := continuous_extension

lemma continuousLinearMapExtension_apply {R E F : Type*} [Semiring R]
    [UniformSpace E] [AddCommGroup E] [IsUniformAddGroup E]
    [Module R E] [UniformContinuousConstSMul R E]
    [UniformSpace F] [AddCommGroup F] [IsUniformAddGroup F]
    [Module R F] [UniformContinuousConstSMul R F] [T2Space F] [CompleteSpace F]
    (f : E ‚ÜíL[R] F) (x : Completion E) :
    Completion.continuousLinearMapExtension R f x = Completion.extension f x := by
  simp [continuousLinearMapExtension]

@[simp]
lemma continuousLinearMapExtension_coe {R E F : Type*} [Semiring R]
    [UniformSpace E] [AddCommGroup E] [IsUniformAddGroup E]
    [Module R E] [UniformContinuousConstSMul R E]
    [UniformSpace F] [AddCommGroup F] [IsUniformAddGroup F]
    [Module R F] [UniformContinuousConstSMul R F] [T2Space F] [CompleteSpace F]
    (f : E ‚ÜíL[R] F) (x : E) :
    Completion.continuousLinearMapExtension R f x = f x := by
  simp [continuousLinearMapExtension, extension_coe f.uniformContinuous]

end UniformSpace.Completion

lemma norm_eval_le_norm_mul_ciSup {E G : Type*}
    [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [NormedAddCommGroup G] [Module ‚Ñù G] [NormSMulClass ‚Ñù G]
    (f : Dual ‚Ñù E ‚Üí‚Çó[‚Ñù] G) {y : E} (hy : ‚àÉ M, ‚àÄ L, ‚Äñf L‚Äñ ‚â§ 1 ‚Üí L y ‚â§ M) (L : Dual ‚Ñù E) :
    ‚ÄñL y‚Äñ ‚â§ ‚Äñf L‚Äñ * ‚®Ü (L') (_ : ‚Äñf L'‚Äñ ‚â§ 1), L' y := by
  have h_bdd : BddAbove (Set.range fun L' ‚Ü¶ ‚®Ü (_ : ‚Äñf L'‚Äñ ‚â§ 1), L' y) := by
    obtain ‚ü®M, hM‚ü© := hy
    refine ‚ü®M, ?_‚ü©
    simp only [mem_upperBounds, Set.mem_range, forall_exists_index, forall_apply_eq_imp_iff]
    intro L
    by_cases hL : ‚Äñf L‚Äñ ‚â§ 1
    ¬∑ simpa [hL] using hM L hL
    ¬∑ simp only [hL, Real.iSup_of_isEmpty]
      simpa using hM 0 (by simp)
  have h L (hL : ‚Äñf L‚Äñ = 1) : ‚ÄñL y‚Äñ ‚â§ ‚Äñf L‚Äñ * ‚®Ü L', ‚®Ü (_ : ‚Äñf L'‚Äñ ‚â§ 1), L' y := by
    rw [hL, one_mul]
    rcases le_total 0 (L y) with hLy | hLy
    ¬∑ exact le_ciSup_of_le h_bdd L (by simp [hL, abs_of_nonneg hLy])
    ¬∑ exact le_ciSup_of_le h_bdd (-L) (by simp [hL, abs_of_nonpos hLy])
  have hL_zero_of_L2 (hL : f L = 0) : L y = 0 := by
    have h_smul (r : ‚Ñù) : f (r ‚Ä¢ L) = 0 := by simp [hL]
    contrapose! hy with hL_zero
    refine fun M ‚Ü¶ ‚ü®((M + 1) / L y) ‚Ä¢ L, by simp [h_smul], ?_‚ü©
    simp [div_mul_cancel‚ÇÄ _ hL_zero]
  by_cases hL_zero : L y = 0
  ¬∑ simp only [hL_zero, norm_zero]
    refine mul_nonneg (by positivity) ?_
    exact le_ciSup_of_le h_bdd 0 (by simp)
  specialize h (‚Äñf L‚Äñ‚Åª¬π ‚Ä¢ L) ?_
  ¬∑ simp only [map_smul, norm_smul, norm_inv, norm_norm]
    rw [inv_mul_cancel‚ÇÄ]
    simp only [ne_eq, norm_eq_zero]
    contrapose! hL_zero
    exact hL_zero_of_L2 hL_zero
  simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul, norm_mul, norm_inv,
    norm_norm, map_smul, norm_smul] at h
  rwa [mul_assoc, mul_le_mul_iff_of_pos_left] at h
  simp only [inv_pos, norm_pos_iff, ne_eq]
  contrapose! hL_zero
  exact hL_zero_of_L2 hL_zero

namespace ProbabilityTheory

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [MeasurableSpace E] [BorelSpace E]
  [CompleteSpace E] [SecondCountableTopology E]
  {Œº : Measure E} {p : ‚Ñù‚â•0‚àû} [Fact (1 ‚â§ p)]

-- added in another PR
omit [SecondCountableTopology E] in
lemma covarianceBilin_apply' [IsFiniteMeasure Œº] (h : MemLp id 2 Œº) (L‚ÇÅ L‚ÇÇ : Dual ‚Ñù E) :
    covarianceBilin Œº L‚ÇÅ L‚ÇÇ = ‚à´ x, L‚ÇÅ (x - Œº[id]) * L‚ÇÇ (x - Œº[id]) ‚àÇŒº := by
  rw [covarianceBilin_apply h]
  have hL (L : Dual ‚Ñù E) : Œº[L] = L (‚à´ x, x ‚àÇŒº) := L.integral_comp_comm (h.integrable (by simp))
  simp [‚Üê hL]

section centeredToLp

/-- The Bochner integral as a continuous linear map from the dual to `‚Ñù`.
This is well defined if the measure has a first moment. If not, it is uniformly zero (since
`Dual.toLp` is zero in that case). -/
noncomputable
def integralDualCLM (Œº : Measure E) : Dual ‚Ñù E ‚ÜíL[‚Ñù] ‚Ñù := L1.integralCLM.comp (Dual.toLp Œº 1)

/-- The function `L ‚Ü¶ L (x - Œº[id])` as a continuous linear map from the dual to `Lp ‚Ñù p Œº`.
This definition takes meaningful values only if the measure has a first moment and a moment of
order `p` (`MemLp id 1 Œº` and `MemLp id p Œº`). -/
noncomputable
def Dual.centeredToLp (Œº : Measure E) [IsFiniteMeasure Œº] (p : ‚Ñù‚â•0‚àû) [Fact (1 ‚â§ p)] :
    Dual ‚Ñù E ‚ÜíL[‚Ñù] Lp ‚Ñù p Œº :=
  Dual.toLp Œº p - (Lp.constL p Œº ‚Ñù).comp (integralDualCLM Œº)

lemma centeredToLp_apply (Œº : Measure E) [IsGaussian Œº] (hp : p ‚â† ‚àû) (L : Dual ‚Ñù E) :
    Dual.centeredToLp Œº p L =·µê[Œº] fun x ‚Ü¶ L (x - ‚à´ z, z ‚àÇŒº) := by
  simp only [Dual.centeredToLp, ContinuousLinearMap.coe_sub', Pi.sub_apply,
    AddSubgroupClass.coe_sub, map_sub]
  filter_upwards [Dual.toLp_apply_ae (IsGaussian.memLp_id Œº p hp) L,
    Lp.coeFn_sub (Dual.toLp Œº p L) ((Lp.constL p Œº ‚Ñù).comp (integralDualCLM Œº) L)] with x hx‚ÇÅ hx‚ÇÇ
  simp only [AddSubgroupClass.coe_sub, Pi.sub_apply] at hx‚ÇÇ
  rw [‚Üê hx‚ÇÅ, hx‚ÇÇ]
  congr
  simp only [integralDualCLM, ContinuousLinearMap.coe_comp', Function.comp_apply,
    Lp.constL_apply, Lp.const_val, AEEqFun.coeFn_const_eq]
  rw [‚Üê L1.integral_eq, L1.integral_eq_integral, ‚Üê IsGaussian.integral_dual]
  refine integral_congr_ae ?_
  exact Dual.toLp_apply_ae (IsGaussian.memLp_id Œº 1 (by simp)) L

lemma norm_centeredToLp [IsGaussian Œº] (L : Dual ‚Ñù E) :
    ‚ÄñDual.centeredToLp Œº 2 L‚Äñ = ‚àö(covarianceBilin Œº L L) := by
  simp only [covarianceBilin_apply' (IsGaussian.memLp_id Œº 2 (by simp)), id_eq]
  rw [norm_eq_sqrt_real_inner]
  congr
  refine integral_congr_ae ?_
  filter_upwards [centeredToLp_apply Œº (by simp : 2 ‚â† ‚àû) L] with x hx
  simp [hx]

lemma sq_norm_centeredToLp [IsGaussian Œº] (L : Dual ‚Ñù E) :
    ‚ÄñDual.centeredToLp Œº 2 L‚Äñ ^ 2 = covarianceBilin Œº L L := by
  rw [norm_centeredToLp, Real.sq_sqrt]
  rw [covarianceBilin_same_eq_variance (IsGaussian.memLp_id Œº 2 (by simp))]
  exact variance_nonneg _ _

end centeredToLp

variable [IsGaussian Œº]

section CameronMartinSpace

/-- The Cameron-Martin space of a Gaussian measure.
This is a separable Hilbert space. -/
noncomputable
abbrev CameronMartin (Œº : Measure E) [IsFiniteMeasure Œº] :=
  Completion (Submodule.map (Dual.centeredToLp Œº 2) ‚ä§)

-- Uncomment the following lines to check that we can synthesize instances for `CameronMartin Œº`:
-- #synth NormedAddCommGroup (CameronMartin Œº)
-- #synth InnerProductSpace ‚Ñù (CameronMartin Œº)
-- #synth CompleteSpace (CameronMartin Œº)

instance (Œº : Measure E) [IsFiniteMeasure Œº] : SecondCountableTopology (CameronMartin Œº) := by
  suffices SecondCountableTopology (Submodule.map (Dual.centeredToLp Œº 2) ‚ä§) by infer_instance
  have : Fact (2 ‚â† ‚àû) := ‚ü®by simp‚ü©
  exact TopologicalSpace.Subtype.secondCountableTopology _

namespace CameronMartin

/-- Inclusion from the dual into the Cameron-Martin space, as a linear map. -/
noncomputable
def ofDual (Œº : Measure E) [IsFiniteMeasure Œº] : Dual ‚Ñù E ‚Üí‚Çó[‚Ñù] CameronMartin Œº :=
  Completion.toComplL.toLinearMap.comp (((Dual.centeredToLp Œº 2).submoduleMap ‚ä§).comp
    (Submodule.topEquiv (R := ‚Ñù) (M := Dual ‚Ñù E)).symm.toLinearMap)

omit [CompleteSpace E] [SecondCountableTopology E] in
lemma ofDual_apply (L : Dual ‚Ñù E) :
    ofDual Œº L
      = (‚ü®Dual.centeredToLp Œº 2 L, Submodule.mem_map.mpr ‚ü®L, by simp, rfl‚ü©‚ü© :
        Submodule.map (Dual.centeredToLp Œº 2) ‚ä§) := rfl

lemma norm_ofDual (L : Dual ‚Ñù E) : ‚ÄñofDual Œº L‚Äñ = ‚àö(covarianceBilin Œº L L) := by
  rw [ofDual_apply]
  simp only [Completion.norm_coe, AddSubgroupClass.coe_norm]
  exact norm_centeredToLp _

lemma sq_norm_ofDual (L : Dual ‚Ñù E) : ‚ÄñofDual Œº L‚Äñ ^ 2 = covarianceBilin Œº L L := by
  rw [norm_ofDual, Real.sq_sqrt]
  rw [covarianceBilin_same_eq_variance (IsGaussian.memLp_id Œº 2 (by simp))]
  exact variance_nonneg _ _

end CameronMartin

end CameronMartinSpace

section EvaluationMap

variable {y : E}

namespace CameronMartinAux -- namespace for auxiliary definitions and lemmas

/-- For an L2 function `x` in the image of `Dual ‚Ñù E` by `Dual.centeredToLp Œº 2`, we can evaluate
`x` at `y : E` by taking `L y` for an arbitrary `L : Dual ‚Ñù E` that is sent to `x`.
This is an auxiliary definition for `CameronMartin.eval`. -/
noncomputable
def evalL2 (Œº : Measure E) [IsGaussian Œº] (y : E) (x : Submodule.map (Dual.centeredToLp Œº 2) ‚ä§) :
    ‚Ñù :=
  (Submodule.mem_map.mp x.2).choose y

lemma norm_eval_le_norm_centeredToLp_mul (hy : ‚àÉ M, ‚àÄ L, covarianceBilin Œº L L ‚â§ 1 ‚Üí L y ‚â§ M)
    (L : Dual ‚Ñù E) :
    ‚ÄñL y‚Äñ ‚â§ ‚ÄñDual.centeredToLp Œº 2 L‚Äñ * ‚®Ü (L') (_ : covarianceBilin Œº L' L' ‚â§ 1), L' y := by
  simp_rw [‚Üê sq_norm_centeredToLp, sq_le_one_iff_abs_le_one, abs_norm] at hy ‚ä¢
  exact norm_eval_le_norm_mul_ciSup (Dual.centeredToLp Œº 2).toLinearMap hy L

lemma norm_evalL2_le (hy : ‚àÉ M, ‚àÄ L, covarianceBilin Œº L L ‚â§ 1 ‚Üí L y ‚â§ M)
    (x : Submodule.map (Dual.centeredToLp Œº 2) ‚ä§) :
    ‚ÄñevalL2 Œº y x‚Äñ ‚â§ ‚Äñx‚Äñ * ‚®Ü (L : Dual ‚Ñù E) (_ : covarianceBilin Œº L L ‚â§ 1), L y := by
  simp only [AddSubgroupClass.coe_norm]
  conv_rhs => rw [‚Üê (Submodule.mem_map.mp x.2).choose_spec.2]
  exact norm_eval_le_norm_centeredToLp_mul hy (Submodule.mem_map.mp x.2).choose

lemma eval_eq_of_centeredToLp_eq (hy : ‚àÉ M, ‚àÄ L, covarianceBilin Œº L L ‚â§ 1 ‚Üí L y ‚â§ M)
    (L L' : Dual ‚Ñù E) (hL : Dual.centeredToLp Œº 2 L = Dual.centeredToLp Œº 2 L') :
    L y = L' y := by
  rw [‚Üê sub_eq_zero, ‚Üê Pi.sub_apply, ‚Üê norm_eq_zero]
  suffices ‚Äñ‚áë(L - L') y‚Äñ = 0 by simpa
  refine le_antisymm ?_ (by positivity)
  refine (norm_eval_le_norm_centeredToLp_mul hy _ (Œº := Œº)).trans ?_
  simp [hL]

lemma evalL2_eq (hy : ‚àÉ M, ‚àÄ L, covarianceBilin Œº L L ‚â§ 1 ‚Üí L y ‚â§ M)
    (x : Submodule.map (Dual.centeredToLp Œº 2) ‚ä§)
    {L : Dual ‚Ñù E} (hL : Dual.centeredToLp Œº 2 L = x) :
    evalL2 Œº y x = L y := by
  rw [evalL2]
  refine eval_eq_of_centeredToLp_eq hy (Submodule.mem_map.mp x.2).choose L ?_
  rw [hL, (Submodule.mem_map.mp x.2).choose_spec.2]

lemma evalL2_centeredToLp_eq (hy : ‚àÉ M, ‚àÄ L, covarianceBilin Œº L L ‚â§ 1 ‚Üí L y ‚â§ M)
    (L : Dual ‚Ñù E) :
    evalL2 Œº y ‚ü®Dual.centeredToLp Œº 2 L, Submodule.mem_map.mpr ‚ü®L, by simp, rfl‚ü©‚ü© = L y :=
  evalL2_eq hy _ (by simp)

end CameronMartinAux

namespace CameronMartin
open CameronMartinAux

/-- Evaluation map on the Cameron-Martin space. `CameronMartin.eval Œº y hy x` is the evaluation of
`x` at `y`, where `x` is an element of the Cameron-Martin space of the Gaussian measure `Œº`.
This map is defined for `y` with bounded Cameron-Martin norm, i.e., such that there exists `M` with
`‚àÄ L : Dual ‚Ñù E, covarianceBilin Œº L L ‚â§ 1 ‚Üí L y ‚â§ M`.
It satisfies `eval Œº y hy (ofDual Œº L) = L y`. -/
noncomputable
def eval (Œº : Measure E) [IsGaussian Œº] (y : E)
    (hy : ‚àÉ M, ‚àÄ L, covarianceBilin Œº L L ‚â§ 1 ‚Üí L y ‚â§ M) :
    Dual ‚Ñù (CameronMartin Œº) :=
  Completion.continuousLinearMapExtension ‚Ñù <|
  LinearMap.mkContinuous
    { toFun x := evalL2 Œº y x
      map_add' x‚ÇÅ x‚ÇÇ := by
        obtain ‚ü®L‚ÇÅ, -, hL‚ÇÅ‚ü© := Submodule.mem_map.mp x‚ÇÅ.2
        obtain ‚ü®L‚ÇÇ, -, hL‚ÇÇ‚ü© := Submodule.mem_map.mp x‚ÇÇ.2
        rw [evalL2_eq hy x‚ÇÅ hL‚ÇÅ, evalL2_eq hy x‚ÇÇ hL‚ÇÇ, evalL2_eq hy (x‚ÇÅ + x‚ÇÇ) (L := L‚ÇÅ + L‚ÇÇ)]
        ¬∑ simp
        ¬∑ simp [hL‚ÇÅ, hL‚ÇÇ]
      map_smul' r x := by
        obtain ‚ü®L, -, hL‚ü© := Submodule.mem_map.mp x.2
        rw [evalL2_eq hy x hL, evalL2_eq hy (r ‚Ä¢ x) (L := r ‚Ä¢ L)]
        ¬∑ simp
        ¬∑ simp [hL] }
    (‚®Ü (L') (_ : covarianceBilin Œº L' L' ‚â§ 1), L' y) fun x ‚Ü¶ by
    simp only [LinearMap.coe_mk, AddHom.coe_mk, AddSubgroupClass.coe_norm]
    rw [mul_comm]
    exact norm_evalL2_le hy x

lemma eval_ofDual (hy : ‚àÉ M, ‚àÄ L, covarianceBilin Œº L L ‚â§ 1 ‚Üí L y ‚â§ M) (L : Dual ‚Ñù E) :
    eval Œº y hy (ofDual Œº L) = L y := by
  rw [ofDual_apply]
  simp only [eval, Completion.continuousLinearMapExtension_coe, LinearMap.mkContinuous_apply,
    LinearMap.coe_mk, AddHom.coe_mk]
  rw [evalL2_centeredToLp_eq hy]

/-- Map from the space on which a Gaussian measure `Œº` is defined to the Cameron-Martin space
of `Œº`. This takes a meaningful value only if the argument has bounded Cameron-Martin norm,
and takes the default value zero otherwise. -/
noncomputable
def ofBounded (Œº : Measure E) [IsGaussian Œº] (y : E)
    [Decidable (‚àÉ M, ‚àÄ L, covarianceBilin Œº L L ‚â§ 1 ‚Üí L y ‚â§ M)] :
    CameronMartin Œº :=
  if hy : ‚àÉ M, ‚àÄ L, covarianceBilin Œº L L ‚â§ 1 ‚Üí L y ‚â§ M
    then (InnerProductSpace.toDual ‚Ñù (CameronMartin Œº)).symm (eval Œº y hy)
    else 0

variable [Decidable (‚àÉ M, ‚àÄ L, covarianceBilin Œº L L ‚â§ 1 ‚Üí L y ‚â§ M)]

lemma ofBounded_def (hy : ‚àÉ M, ‚àÄ L, covarianceBilin Œº L L ‚â§ 1 ‚Üí L y ‚â§ M) :
    ofBounded Œº y = (InnerProductSpace.toDual ‚Ñù (CameronMartin Œº)).symm (eval Œº y hy) := by
  simp [ofBounded, hy]

lemma eval_apply (hy : ‚àÉ M, ‚àÄ L, covarianceBilin Œº L L ‚â§ 1 ‚Üí L y ‚â§ M) (x : CameronMartin Œº) :
    eval Œº y hy x = ‚ü™x, ofBounded Œº y‚ü´_‚Ñù := by
  rw [ofBounded_def hy, real_inner_comm, InnerProductSpace.toDual_symm_apply]

end CameronMartin

end EvaluationMap

section ToInitialSpace

namespace CameronMartinAux -- namespace for auxiliary definitions and lemmas

/-- From `x` in the image of `Dual ‚Ñù E` by `Dual.centeredToLp Œº 2`, we define a point of `E` by
`‚à´ y, L (y - ‚à´ z, z ‚àÇŒº) ‚Ä¢ (y - ‚à´ z, z ‚àÇŒº) ‚àÇŒº` for an arbitrary `L : Dual ‚Ñù E` with
`Dual.centeredToLp Œº 2 L = x`.
This is an auxiliary definition for `CameronMartin.toInitialSpace`. -/
noncomputable
def toInit (Œº : Measure E) [IsFiniteMeasure Œº] (x : Submodule.map (Dual.centeredToLp Œº 2) ‚ä§) : E :=
  ‚à´ y, (Submodule.mem_map.mp x.2).choose (y - ‚à´ z, z ‚àÇŒº) ‚Ä¢ (y - ‚à´ z, z ‚àÇŒº) ‚àÇŒº

lemma toInit_eq (x : Submodule.map (Dual.centeredToLp Œº 2) ‚ä§) {L : Dual ‚Ñù E}
    (hL : Dual.centeredToLp Œº 2 L = x) :
    toInit Œº x = ‚à´ y, L (y - ‚à´ z, z ‚àÇŒº) ‚Ä¢ (y - ‚à´ z, z ‚àÇŒº) ‚àÇŒº :=
  calc toInit Œº x
  _ = ‚à´ y, x.1 y ‚Ä¢ (y - ‚à´ z, z ‚àÇŒº) ‚àÇŒº := by
    rw [toInit]
    conv_rhs => rw [‚Üê (Submodule.mem_map.mp x.2).choose_spec.2]
    refine integral_congr_ae ?_
    filter_upwards [centeredToLp_apply Œº (by simp : 2 ‚â† ‚àû) (Submodule.mem_map.mp x.2).choose]
      with y hy
    rw [hy]
  _ = ‚à´ y, Dual.centeredToLp Œº 2 L y ‚Ä¢ (y - ‚à´ z, z ‚àÇŒº) ‚àÇŒº := by rw [hL]
  _ = ‚à´ y, L (y - ‚à´ z, z ‚àÇŒº) ‚Ä¢ (y - ‚à´ z, z ‚àÇŒº) ‚àÇŒº := by
    refine integral_congr_ae ?_
    filter_upwards [centeredToLp_apply Œº (by simp : 2 ‚â† ‚àû) L] with y hy using by rw [hy]

lemma apply_toInit (x : Submodule.map (Dual.centeredToLp Œº 2) ‚ä§) (L : Dual ‚Ñù E) :
    L (toInit Œº x)
      = ‚à´ y, (Submodule.mem_map.mp x.2).choose (y - ‚à´ z, z ‚àÇŒº) * L (y - ‚à´ z, z ‚àÇŒº) ‚àÇŒº := by
  rw [toInit, ‚Üê L.integral_comp_comm]
  ¬∑ simp
  rw [‚Üê integrable_norm_iff (by fun_prop)]
  simp only [Submodule.mem_top, true_and, map_sub, norm_smul]
  refine MemLp.integrable_mul (p := 2) (q := 2) ?_ ?_
  ¬∑ rw [memLp_norm_iff (by fun_prop)]
    exact MemLp.sub (IsGaussian.memLp_dual Œº _ 2 (by simp)) (memLp_const _)
  ¬∑ rw [memLp_norm_iff (by fun_prop)]
    exact MemLp.sub (IsGaussian.memLp_id Œº 2 (by simp)) (memLp_const _)

lemma apply_toInit_eq_inner (x : Submodule.map (Dual.centeredToLp Œº 2) ‚ä§) (L : Dual ‚Ñù E) :
    L (toInit Œº x) = ‚ü™Dual.centeredToLp Œº 2 L, x‚ü´_‚Ñù := by
  rw [‚Üê (Submodule.mem_map.mp x.2).choose_spec.2, L2.inner_def, apply_toInit]
  simp only [RCLike.inner_apply, conj_trivial]
  refine integral_congr_ae ?_
  filter_upwards [centeredToLp_apply Œº (by simp : 2 ‚â† ‚àû) L,
    centeredToLp_apply Œº (by simp : 2 ‚â† ‚àû) (Submodule.mem_map.mp x.2).choose] with y hy‚ÇÅ hy‚ÇÇ
  rw [hy‚ÇÅ, hy‚ÇÇ]

lemma norm_toInit_le (x : Submodule.map (Dual.centeredToLp Œº 2) ‚ä§) :
    ‚ÄñtoInit Œº x‚Äñ ‚â§ ‚ÄñDual.centeredToLp Œº 2‚Äñ * ‚Äñx‚Äñ := by
  refine norm_le_dual_bound ‚Ñù _ (by positivity) fun L ‚Ü¶ ?_
  calc ‚ÄñL (toInit Œº x)‚Äñ
  _ = ‚Äñ‚ü™Dual.centeredToLp Œº 2 L, x‚ü´_‚Ñù‚Äñ := by rw [apply_toInit_eq_inner]
  _ ‚â§ ‚ÄñDual.centeredToLp Œº 2 L‚Äñ * ‚Äñx‚Äñ := norm_inner_le_norm ((Dual.centeredToLp Œº 2) L) x
  _ ‚â§ ‚ÄñDual.centeredToLp Œº 2‚Äñ * ‚ÄñL‚Äñ * ‚Äñx‚Äñ := by
    gcongr
    exact (Dual.centeredToLp Œº 2).le_opNorm L
  _ = ‚ÄñDual.centeredToLp Œº 2‚Äñ * ‚Äñx‚Äñ * ‚ÄñL‚Äñ := by ring

end CameronMartinAux

namespace CameronMartin
open CameronMartinAux

/-- Continuous linear map from the Cameron-Martin space of a Gaussian measure to the space on
which that measure is defined. This map is injective: see `toInitialSpace_injective`.
Therefore, we can see the Cameron-Martin space as a subspace of the initial space, with a different
norm. -/
noncomputable
def toInitialSpace (Œº : Measure E) [IsGaussian Œº] : CameronMartin Œº ‚ÜíL[‚Ñù] E :=
  Completion.continuousLinearMapExtension ‚Ñù <|
  LinearMap.mkContinuous
    { toFun x := toInit Œº x
      map_add' x y := by
        refine (eq_iff_forall_dual_eq (ùïú := ‚Ñù)).mpr fun L ‚Ü¶ ?_
        simp_rw [map_add, apply_toInit_eq_inner, Submodule.coe_add, inner_add_right]
      map_smul' r x := by
        refine (eq_iff_forall_dual_eq (ùïú := ‚Ñù)).mpr fun L ‚Ü¶ ?_
        simp_rw [map_smul, apply_toInit_eq_inner, Submodule.coe_smul, inner_smul_right]
        simp }
    ‚ÄñDual.centeredToLp Œº 2‚Äñ norm_toInit_le

lemma apply_toInitialSpace_eq_inner (x : CameronMartin Œº) (L : Dual ‚Ñù E) :
    L (toInitialSpace Œº x) = ‚ü™CameronMartin.ofDual Œº L, x‚ü´_‚Ñù := by
  simp only [toInitialSpace, Completion.continuousLinearMapExtension_apply]
  revert x
  rw [‚Üê funext_iff]
  refine Completion.ext (by fun_prop) (by fun_prop) fun x ‚Ü¶ ?_
  rw [Completion.extension_coe (ContinuousLinearMap.uniformContinuous _)]
  simp only [LinearMap.mkContinuous_apply, LinearMap.coe_mk, AddHom.coe_mk]
  rw [apply_toInit_eq_inner, CameronMartin.ofDual_apply, Completion.inner_coe]
  rfl

lemma eq_zero_of_toInitialSpace_eq_zero {x : CameronMartin Œº}
    (h : toInitialSpace Œº x = 0) :
    x = 0 := by
  suffices ‚àÄ y : CameronMartin Œº, ‚ü™y, x‚ü´_‚Ñù = 0 by
    rw [‚Üê inner_self_eq_zero (ùïú := ‚Ñù) (x := x)]
    exact this x
  rw [‚Üê funext_iff]
  refine Completion.ext (by fun_prop) (by fun_prop) fun L ‚Ü¶ ?_
  obtain ‚ü®L', -, hL'‚ü© := Submodule.mem_map.mp L.2
  have : CameronMartin.ofDual Œº L' = L := by rw [CameronMartin.ofDual_apply]; congr
  rw [‚Üê this, ‚Üê apply_toInitialSpace_eq_inner, h]
  simp

lemma toInitialSpace_injective : Function.Injective (toInitialSpace Œº) := by
  intro x y h
  rw [‚Üê sub_eq_zero, ‚Üê map_sub] at h
  rw [‚Üê sub_eq_zero, eq_zero_of_toInitialSpace_eq_zero h]

/-- Any point of the Cameron-Martin space has finite Cameron-Martin norm
`‚®Ü L (_ : covarianceBilin L L ‚â§ 1), L x` (when seen as a point of the initial space). -/
lemma apply_toInitialSpace_le_norm (x : CameronMartin Œº)
    {L : Dual ‚Ñù E} (hL : covarianceBilin Œº L L ‚â§ 1) :
    L (toInitialSpace Œº x) ‚â§ ‚Äñx‚Äñ := by
  calc L (toInitialSpace Œº x)
  _ = ‚ü™ofDual Œº L, x‚ü´_‚Ñù := apply_toInitialSpace_eq_inner x L
  _ ‚â§ ‚ÄñofDual Œº L‚Äñ * ‚Äñx‚Äñ := real_inner_le_norm (ofDual Œº L) x
  _ = ‚àö(covarianceBilin Œº L L) * ‚Äñx‚Äñ := by rw [norm_ofDual]
  _ ‚â§ 1 * ‚Äñx‚Äñ := by gcongr; exact Real.sqrt_le_one.mpr hL
  _ = ‚Äñx‚Äñ := by rw [one_mul]

end CameronMartin

end ToInitialSpace

namespace CameronMartin

variable {y : E} [Decidable (‚àÉ M, ‚àÄ L, covarianceBilin Œº L L ‚â§ 1 ‚Üí L y ‚â§ M)]

@[simp]
lemma toInitialSpace_ofBounded (hy : ‚àÉ M, ‚àÄ L, covarianceBilin Œº L L ‚â§ 1 ‚Üí L y ‚â§ M) :
    toInitialSpace Œº (ofBounded Œº y) = y := by
  refine (eq_iff_forall_dual_eq (ùïú := ‚Ñù)).mpr fun L ‚Ü¶ ?_
  rw [‚Üê eval_ofDual hy, apply_toInitialSpace_eq_inner, eval_apply]

@[simp]
lemma ofBounded_toInitialSpace (x : CameronMartin Œº)
    [Decidable (‚àÉ M, ‚àÄ L, covarianceBilin Œº L L ‚â§ 1 ‚Üí L (toInitialSpace Œº x) ‚â§ M)] :
    ofBounded Œº (toInitialSpace Œº x) = x := by
  refine toInitialSpace_injective ?_
  rw [toInitialSpace_ofBounded ‚ü®‚Äñx‚Äñ, fun _ hL ‚Ü¶ apply_toInitialSpace_le_norm x hL‚ü©]

lemma ofDual_inner_le_of_norm_ofDual_le (x : CameronMartin Œº) {L : Dual ‚Ñù E}
    (hL : ‚ÄñofDual Œº L‚Äñ ‚â§ 1) :
    ‚ü™ofDual Œº L, x‚ü´_‚Ñù ‚â§ ‚®Ü (L) (_ : covarianceBilin Œº L L ‚â§ 1), L (toInitialSpace Œº x) := by
  refine le_ciSup_of_le ?_ L ?_
  ¬∑ refine ‚ü®‚Äñx‚Äñ, ?_‚ü©
    simp only [mem_upperBounds, Set.mem_range, forall_exists_index, forall_apply_eq_imp_iff]
    intro L
    by_cases hL : covarianceBilin Œº L L ‚â§ 1
    ¬∑ simpa [hL] using apply_toInitialSpace_le_norm x hL
    ¬∑ simp [hL]
  have hL' : covarianceBilin Œº L L ‚â§ 1 := by rwa [CameronMartin.norm_ofDual, Real.sqrt_le_one] at hL
  simp only [hL', ciSup_unique]
  rw [‚Üê apply_toInitialSpace_eq_inner]

lemma ofDual_inner_le_mul (x : CameronMartin Œº) (L : Dual ‚Ñù E) :
    ‚ü™ofDual Œº L, x‚ü´_‚Ñù
      ‚â§ ‚ÄñofDual Œº L‚Äñ * ‚®Ü (L) (_ : covarianceBilin Œº L L ‚â§ 1), L (toInitialSpace Œº x) := by
  by_cases h_zero : ‚ÄñofDual Œº L‚Äñ = 0
  ¬∑ simp only [h_zero, zero_mul]
    rw [inner_eq_zero_of_left _ h_zero]
  have h := ofDual_inner_le_of_norm_ofDual_le x (L := ‚ÄñofDual Œº L‚Äñ‚Åª¬π ‚Ä¢ L) ?_
  ¬∑ simp only [map_smul, inner_smul_left, map_inv‚ÇÄ, conj_trivial] at h
    rwa [inv_mul_le_iff‚ÇÄ (by positivity)] at h
  ¬∑ simp only [map_smul, norm_smul, norm_inv, norm_norm]
    exact inv_mul_le_one

lemma inner_le_mul_ciSup (x y : CameronMartin Œº) :
    ‚ü™y, x‚ü´_‚Ñù ‚â§ ‚Äñy‚Äñ * ‚®Ü (L) (_ : covarianceBilin Œº L L ‚â§ 1), L (toInitialSpace Œº x) := by
  induction y using Completion.induction_on with
  | hp =>
    exact isClosed_le (by fun_prop) (by fun_prop)
  | ih a =>
    obtain ‚ü®L, -, hL‚ü© := Submodule.mem_map.mp a.2
    have : (a : CameronMartin Œº) = CameronMartin.ofDual Œº L := by
      simp_rw [CameronMartin.ofDual_apply, hL]
    rw [this]
    exact ofDual_inner_le_mul x L

lemma norm_eq_ciSup (x : CameronMartin Œº) :
    ‚Äñx‚Äñ = ‚®Ü (L) (_ : covarianceBilin Œº L L ‚â§ 1), L (toInitialSpace Œº x) := by
  refine le_antisymm ?_ ?_
  ¬∑ refine InnerProductSpace.norm_le_dual_bound x ?_ fun y ‚Ü¶ ?_
    ¬∑ by_cases h_bdd :
          BddAbove (Set.range fun L ‚Ü¶ ‚®Ü (_ : covarianceBilin Œº L L ‚â§ 1), L (toInitialSpace Œº x))
      ¬∑ exact le_ciSup_of_le h_bdd 0 (by simp)
      ¬∑ simp [h_bdd]
    rw [real_inner_comm, mul_comm]
    exact inner_le_mul_ciSup x y
  ¬∑ refine ciSup_le fun L ‚Ü¶ ?_
    by_cases hL : covarianceBilin Œº L L ‚â§ 1
    ¬∑ simpa [hL] using apply_toInitialSpace_le_norm x hL
    ¬∑ simp [hL]

lemma norm_ofBounded {y : E} [Decidable (‚àÉ M, ‚àÄ L, covarianceBilin Œº L L ‚â§ 1 ‚Üí L y ‚â§ M)]
    (hy : ‚àÉ M, ‚àÄ L, covarianceBilin Œº L L ‚â§ 1 ‚Üí L y ‚â§ M) :
    ‚ÄñofBounded Œº y‚Äñ = ‚®Ü (L) (_ : covarianceBilin Œº L L ‚â§ 1), L y := by
  simp [norm_eq_ciSup, toInitialSpace_ofBounded hy]

end CameronMartin

end ProbabilityTheory
