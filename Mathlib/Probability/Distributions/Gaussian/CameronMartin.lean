/-
Copyright (c) 2025 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
module

public import Mathlib.Analysis.InnerProductSpace.Completion
public import Mathlib.MeasureTheory.Measure.SeparableMeasure
public import Mathlib.Probability.Distributions.Gaussian.CompletionResultsToBeMoved
public import Mathlib.Probability.Distributions.Gaussian.Fernique
public import Mathlib.Probability.Moments.CovarianceBilin
public import Mathlib.Topology.Algebra.Module.ClosedSubmodule

/-!
# Cameron-Martin space

TODO: this is not up to date with the latest use of the rkhs for the def

A Gaussian measure `Œº` on a Banach space `E` is characterized by a separable Hilbert space,
called the Cameron-Martin space of `Œº`.
That space is a subspace of `E`, but with a different norm.

In this file, we define a type `cameronMartin Œº` and build a bijective continuous linear map from
that type to the subset of `E` of points `y` such that the quantity
`‚®Ü (L : StrongDual ‚Ñù E) (_ : Var[L; Œº] ‚â§ 1), L y` is finite.

Since `Œº` has finite second moment, for every function `L : StrongDual ‚Ñù E`, the function
`L ‚Ü¶ L (x - Œº[id])` can be seen as a function in L2.
The subspace of L2 we obtain that way inherits the scalar product of L2, which is equal to the
covariance `covarianceBilin Œº L L'`.
We define `cameronMartin Œº` as the completion of that subspace. It is a separable Hilbert space.

This is the RKHS construction of the Cameron-Martin space. Another construction would be to
consider the subspace of `E` consisting of all points `y` such that the quantity
`‚®Ü (L : StrongDual ‚Ñù E) (_ : Var[L; Œº] ‚â§ 1), L y` is finite, and to endow it with
the norm `‚Äñy‚Äñ = ‚®Ü (L : StrongDual ‚Ñù E) (_ : Var[L; Œº] ‚â§ 1), L y`.
Here we don't define the Cameron-Martin space as a subspace because it would inherit
the norm from `E`, which is not the norm we want to put on it. And we don't want to have two norms
on the same type.

## Main definitions

* `cameronMartin Œº`: Cameron-Martin space of the measure `Œº`.
* `cmOfDual Œº L`: inclusion of the dual space `StrongDual ‚Ñù E` into the Cameron-Martin space.
* `cmCoe`: the continuous linear map from the Cameron-Martin space
  to the initial space `E`. It is injective and its range is the subspace of `E` of points
  `y` such that `‚®Ü (L : StrongDual ‚Ñù E) (_ : Var[L; Œº] ‚â§ 1), L y` is finite.
* `cmOfBounded`: the inverse of `cmCoe`, which takes a point `y : E` with bounded
  Cameron-Martin norm and returns a point of `cameronMartin Œº`.

## Main statements

* `range_cmCoe`: the range of `cmCoe` is the set `{y : E | ‚àÉ M, ‚àÄ L, Var[L; Œº] ‚â§ 1 ‚Üí L y ‚â§ M}`.
* `cmCoe_cmOfBounded` and `cmOfBounded_cmCoe`: the two maps `cmCoe` and `cmOfBounded` are inverses
  of each other.

* `norm_cameronMartin_eq_ciSup`: for `x` in the Cameron-Martin space,
  `‚Äñx‚Äñ = ‚®Ü (L) (_ : Var[L; Œº] ‚â§ 1), L (cmCoe x)`.
* `norm_cmOfBounded`: for `y` in `E` with bounded Cameron-Martin norm,
  `‚ÄñcmOfBounded Œº y‚Äñ = ‚®Ü (L) (_ : Var[L; Œº] ‚â§ 1), L y`.

## Implementation details

We build the Cameron-Martin space for any finite measure with a finite second moment, not only for
Gaussian measures. We do so only because we can write the definition with that weaker hypothesis:
we are not aware of any use of the Cameron-Martin space for non-Gaussian measures.

## References

* [F. Bar, *Quuxes*][bibkey]

-/

@[expose] public section

open MeasureTheory NormedSpace UniformSpace
open scoped ENNReal InnerProductSpace

variable {M R F : Type*} [Ring R] [NormedAddCommGroup M] [Module R M]
    [CompleteSpace M] [UniformContinuousConstSMul R M]
    [UniformSpace F] [AddCommGroup F] [Module R F] [T2Space F] [CompleteSpace F]
    {s : Submodule R M}

namespace ProbabilityTheory

/-- A finite measure has two moments if `‚à´ x, x ^ 2 ‚àÇŒº < ‚àû`, that is if `MemLp id 2 Œº`. -/
class HasTwoMoments {E : Type*} {_ : MeasurableSpace E} [ENorm E] [TopologicalSpace E]
    (Œº : Measure E) extends IsFiniteMeasure Œº where
  memLp_two : MemLp id 2 Œº

lemma memLp_two_id {E : Type*} {_ : MeasurableSpace E} [ENorm E] [TopologicalSpace E]
    {Œº : Measure E} [HasTwoMoments Œº] : MemLp id 2 Œº := HasTwoMoments.memLp_two

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [MeasurableSpace E] [BorelSpace E]
  [CompleteSpace E]
  {Œº : Measure E} {p : ‚Ñù‚â•0‚àû} [Fact (1 ‚â§ p)] {y : E}

instance [SecondCountableTopology E] [IsGaussian Œº] : HasTwoMoments Œº where
  memLp_two := IsGaussian.memLp_id Œº 2 (by simp)

lemma _root_.ContinuousLinearMap.memLp_two {E : Type*}
    [NormedAddCommGroup E] [NormedSpace ‚Ñù E] {_ : MeasurableSpace E}
    {Œº : Measure E} [HasTwoMoments Œº] (L : StrongDual ‚Ñù E) :
    MemLp L 2 Œº := L.comp_memLp' memLp_two_id

section CameronMartinSpace

/-- The Cameron-Martin space of a Gaussian measure.
This is a separable Hilbert space. -/
noncomputable
def cameronMartin (Œº : Measure E) [HasTwoMoments Œº] : Submodule ‚Ñù (Lp ‚Ñù 2 Œº) :=
  (LinearMap.range (StrongDual.centeredToLp Œº 2)).topologicalClosure

variable [HasTwoMoments Œº]

noncomputable
instance : Coe (LinearMap.range (StrongDual.centeredToLp Œº 2)) (cameronMartin Œº) := ‚ü®coeClosure‚ü©

noncomputable
instance instCoeFun : CoeFun (cameronMartin Œº) (fun _ ‚Ü¶ E ‚Üí ‚Ñù) := ‚ü®fun f ‚Ü¶ (f : E ‚Üí ‚Ñù)‚ü©

noncomputable instance : NormedAddCommGroup (cameronMartin Œº) := by
  unfold cameronMartin
  infer_instance

noncomputable instance : InnerProductSpace ‚Ñù (cameronMartin Œº) := by
  unfold cameronMartin
  infer_instance

noncomputable instance : CompleteSpace (cameronMartin Œº) := by
  unfold cameronMartin
  infer_instance

instance [SecondCountableTopology E] (Œº : Measure E) [HasTwoMoments Œº] :
    SecondCountableTopology (cameronMartin Œº) := by
  have : Fact (2 ‚â† ‚àû) := ‚ü®by simp‚ü©
  exact TopologicalSpace.Subtype.secondCountableTopology _

/-- Inclusion from the StrongDual into the Cameron-Martin space, as a linear map. -/
noncomputable
def cmOfDual (Œº : Measure E) [HasTwoMoments Œº] : StrongDual ‚Ñù E ‚Üí‚Çó[‚Ñù] cameronMartin Œº :=
  (toClosureCLM _).toLinearMap.comp ((StrongDual.centeredToLp Œº 2).toLinearMap.rangeRestrict)

noncomputable
instance : Coe (StrongDual ‚Ñù E) (cameronMartin Œº) := ‚ü®cmOfDual Œº‚ü©

omit [CompleteSpace E] in
lemma cmOfDual_apply (L : StrongDual ‚Ñù E) :
    cmOfDual Œº L = (‚ü®StrongDual.centeredToLp Œº 2 L, LinearMap.mem_range.mpr ‚ü®L, rfl‚ü©‚ü© :
        LinearMap.range (StrongDual.centeredToLp Œº 2)) := rfl

lemma cmOfDual_inner (L‚ÇÅ L‚ÇÇ : StrongDual ‚Ñù E) :
    ‚ü™cmOfDual Œº L‚ÇÅ, cmOfDual Œº L‚ÇÇ‚ü´_‚Ñù = covarianceBilinDual Œº L‚ÇÅ L‚ÇÇ := by
  simp only [cmOfDual_apply]
  exact StrongDual.centeredToLp_two_inner memLp_two_id L‚ÇÅ L‚ÇÇ

lemma norm_cmOfDual (L : StrongDual ‚Ñù E) : ‚ÄñcmOfDual Œº L‚Äñ = ‚àöVar[L; Œº] := by
  rw [norm_eq_sqrt_real_inner, cmOfDual_inner, covarianceBilinDual_self_eq_variance memLp_two_id]

lemma sq_norm_cmOfDual (L : StrongDual ‚Ñù E) : ‚ÄñcmOfDual Œº L‚Äñ ^ 2 = Var[L; Œº] := by
  rw [‚Üê real_inner_self_eq_norm_sq, cmOfDual_inner,
    covarianceBilinDual_self_eq_variance memLp_two_id]

end CameronMartinSpace

section cmOfBounded

/-! We build a map from the elements of `E` with finite Cameron-Martin norm to
the Cameron-Martin space. -/

variable [HasTwoMoments Œº]

namespace CameronMartinAux -- namespace for auxiliary definitions and lemmas

/-- For an L2 function `x` in the image of `Dual ‚Ñù E` by `Dual.centeredToLp Œº 2`, we can evaluate
`x` at `y : E` by taking `L y` for an arbitrary `L : StrongDual ‚Ñù E` that is sent to `x`.
This is an auxiliary definition for `CameronMartin.eval`. -/
noncomputable
def evalL2 (Œº : Measure E) [HasTwoMoments Œº] (y : E)
    (x : LinearMap.range (StrongDual.centeredToLp Œº 2)) : ‚Ñù :=
  (LinearMap.mem_range.mp x.2).choose y

lemma norm_eval_le_norm_centeredToLp_mul (hy : ‚àÉ M, ‚àÄ L : StrongDual ‚Ñù E, Var[L; Œº] ‚â§ 1 ‚Üí L y ‚â§ M)
    (L : StrongDual ‚Ñù E) :
    ‚ÄñL y‚Äñ ‚â§ ‚ÄñStrongDual.centeredToLp Œº 2 L‚Äñ
      * ‚®Ü (L' : StrongDual ‚Ñù E) (_ : Var[L'; Œº] ‚â§ 1), L' y := by
  simp_rw [‚Üê StrongDual.sq_norm_centeredToLp_two memLp_two_id,
    sq_le_one_iff_abs_le_one, abs_norm] at hy ‚ä¢
  exact norm_eval_le_norm_mul_ciSup (StrongDual.centeredToLp Œº 2).toLinearMap hy L

lemma norm_evalL2_le (hy : ‚àÉ M, ‚àÄ L : StrongDual ‚Ñù E, Var[L; Œº] ‚â§ 1 ‚Üí L y ‚â§ M)
    (x : LinearMap.range (StrongDual.centeredToLp Œº 2)) :
    ‚ÄñevalL2 Œº y x‚Äñ ‚â§ ‚Äñx‚Äñ * ‚®Ü (L : StrongDual ‚Ñù E) (_ : Var[L; Œº] ‚â§ 1), L y := by
  simp only [AddSubgroupClass.coe_norm]
  conv_rhs => rw [‚Üê (LinearMap.mem_range.mp x.2).choose_spec]
  exact norm_eval_le_norm_centeredToLp_mul hy (LinearMap.mem_range.mp x.2).choose

lemma eval_eq_of_centeredToLp_eq (hy : ‚àÉ M, ‚àÄ L : StrongDual ‚Ñù E, Var[L; Œº] ‚â§ 1 ‚Üí L y ‚â§ M)
    (L L' : StrongDual ‚Ñù E) (hL : StrongDual.centeredToLp Œº 2 L = StrongDual.centeredToLp Œº 2 L') :
    L y = L' y := by
  rw [‚Üê sub_eq_zero, ‚Üê Pi.sub_apply, ‚Üê norm_eq_zero]
  suffices ‚Äñ‚áë(L - L') y‚Äñ = 0 by simpa
  refine le_antisymm ?_ (by positivity)
  refine (norm_eval_le_norm_centeredToLp_mul hy _ (Œº := Œº)).trans ?_
  simp [hL]

lemma evalL2_eq (hy : ‚àÉ M, ‚àÄ L : StrongDual ‚Ñù E, Var[L; Œº] ‚â§ 1 ‚Üí L y ‚â§ M)
    (x : LinearMap.range (StrongDual.centeredToLp Œº 2))
    {L : StrongDual ‚Ñù E} (hL : StrongDual.centeredToLp Œº 2 L = x) :
    evalL2 Œº y x = L y := by
  rw [evalL2]
  refine eval_eq_of_centeredToLp_eq hy (LinearMap.mem_range.mp x.2).choose L ?_
  rw [hL, (LinearMap.mem_range.mp x.2).choose_spec]

lemma evalL2_centeredToLp_eq (hy : ‚àÉ M, ‚àÄ L : StrongDual ‚Ñù E, Var[L; Œº] ‚â§ 1 ‚Üí L y ‚â§ M)
    (L : StrongDual ‚Ñù E) :
    evalL2 Œº y ‚ü®StrongDual.centeredToLp Œº 2 L, LinearMap.mem_range.mpr ‚ü®L, rfl‚ü©‚ü© = L y :=
  evalL2_eq hy _ (by simp)

end CameronMartinAux

open CameronMartinAux

/-- Evaluation map on the Cameron-Martin space. `CameronMartin.eval Œº y hy x` is the evaluation of
`x` at `y`, where `x` is an element of the Cameron-Martin space of the Gaussian measure `Œº`.
This map is defined for `y` with bounded Cameron-Martin norm, i.e., such that there exists `M` with
`‚àÄ L : StrongDual ‚Ñù E, Var[L; Œº] ‚â§ 1 ‚Üí L y ‚â§ M`.
It satisfies `cmEval Œº y hy (ofDual Œº L) = L y`. -/
noncomputable
def cmEval (Œº : Measure E) [HasTwoMoments Œº] (y : E)
    (hy : ‚àÉ M, ‚àÄ L : StrongDual ‚Ñù E, Var[L; Œº] ‚â§ 1 ‚Üí L y ‚â§ M) :
    StrongDual ‚Ñù (cameronMartin Œº) :=
  closureExtensionCLM (LinearMap.range (StrongDual.centeredToLp Œº 2)) <|
  LinearMap.mkContinuous
    { toFun x := evalL2 Œº y x
      map_add' x‚ÇÅ x‚ÇÇ := by
        obtain ‚ü®L‚ÇÅ, hL‚ÇÅ‚ü© := LinearMap.mem_range.mp x‚ÇÅ.2
        obtain ‚ü®L‚ÇÇ, hL‚ÇÇ‚ü© := LinearMap.mem_range.mp x‚ÇÇ.2
        rw [evalL2_eq hy x‚ÇÅ hL‚ÇÅ, evalL2_eq hy x‚ÇÇ hL‚ÇÇ, evalL2_eq hy (x‚ÇÅ + x‚ÇÇ) (L := L‚ÇÅ + L‚ÇÇ)]
        ¬∑ simp
        ¬∑ simp [hL‚ÇÅ, hL‚ÇÇ]
      map_smul' r x := by
        obtain ‚ü®L, hL‚ü© := LinearMap.mem_range.mp x.2
        rw [evalL2_eq hy x hL, evalL2_eq hy (r ‚Ä¢ x) (L := r ‚Ä¢ L)]
        ¬∑ simp
        ¬∑ simp [hL] }
    (‚®Ü (L' : StrongDual ‚Ñù E) (_ : Var[L'; Œº] ‚â§ 1), L' y) fun x ‚Ü¶ by
    simp only [LinearMap.coe_mk, AddHom.coe_mk, AddSubgroupClass.coe_norm]
    rw [mul_comm]
    exact norm_evalL2_le hy x

lemma cmEval_cmOfDual (hy : ‚àÉ M, ‚àÄ L : StrongDual ‚Ñù E, Var[L; Œº] ‚â§ 1 ‚Üí L y ‚â§ M)
    (L : StrongDual ‚Ñù E) :
    cmEval Œº y hy (cmOfDual Œº L) = L y := by
  rw [cmOfDual_apply, cmEval]
  unfold cameronMartin
  simp only [closureExtensionCLM_coe, LinearMap.mkContinuous_apply, LinearMap.coe_mk, AddHom.coe_mk]
  rw [evalL2_centeredToLp_eq hy]

/-- Map from the space on which a Gaussian measure `Œº` is defined to the Cameron-Martin space
of `Œº`. This takes a meaningful value only if the argument has bounded Cameron-Martin norm,
and takes the default value zero otherwise. -/
noncomputable
def cmOfBounded (Œº : Measure E) [HasTwoMoments Œº] (y : E)
    [Decidable (‚àÉ M, ‚àÄ L : StrongDual ‚Ñù E, Var[L; Œº] ‚â§ 1 ‚Üí L y ‚â§ M)] :
    cameronMartin Œº :=
  if hy : ‚àÉ M, ‚àÄ L : StrongDual ‚Ñù E, Var[L; Œº] ‚â§ 1 ‚Üí L y ‚â§ M
    then (InnerProductSpace.toDual ‚Ñù (cameronMartin Œº)).symm (cmEval Œº y hy)
    else 0

variable [Decidable (‚àÉ M, ‚àÄ L : StrongDual ‚Ñù E, Var[L; Œº] ‚â§ 1 ‚Üí L y ‚â§ M)]

lemma cmOfBounded_def (hy : ‚àÉ M, ‚àÄ L : StrongDual ‚Ñù E, Var[L; Œº] ‚â§ 1 ‚Üí L y ‚â§ M) :
    cmOfBounded Œº y = (InnerProductSpace.toDual ‚Ñù (cameronMartin Œº)).symm (cmEval Œº y hy) := by
  simp [cmOfBounded, hy]

lemma cmEval_apply (hy : ‚àÉ M, ‚àÄ L : StrongDual ‚Ñù E, Var[L; Œº] ‚â§ 1 ‚Üí L y ‚â§ M) (x : cameronMartin Œº) :
    cmEval Œº y hy x = ‚ü™x, cmOfBounded Œº y‚ü´_‚Ñù := by
  rw [cmOfBounded_def hy, real_inner_comm, InnerProductSpace.toDual_symm_apply]

end cmOfBounded

section CmCoe

/-! We build an injective continuous linear map from the Cameron-Martin space to the elements
of `E` with finite Cameron-Martin norm. This is an inverse of `CameronMartin.cmOfBounded`. -/

variable [SecondCountableTopology E] [HasTwoMoments Œº]

namespace CameronMartinAux -- namespace for auxiliary definitions and lemmas

/-- From `x` in the image of `Dual ‚Ñù E` by `Dual.centeredToLp Œº 2`, we define a point of `E` by
`‚à´ y, L (y - ‚à´ z, z ‚àÇŒº) ‚Ä¢ (y - ‚à´ z, z ‚àÇŒº) ‚àÇŒº` for an arbitrary `L : StrongDual ‚Ñù E` with
`Dual.centeredToLp Œº 2 L = x`.
This is an auxiliary definition for `CameronMartin.cmCoe`. -/
noncomputable
def toInit (Œº : Measure E) [IsFiniteMeasure Œº]
    (x : LinearMap.range (StrongDual.centeredToLp Œº 2)) : E :=
  ‚à´ y, (LinearMap.mem_range.mp x.2).choose (y - ‚à´ z, z ‚àÇŒº) ‚Ä¢ (y - ‚à´ z, z ‚àÇŒº) ‚àÇŒº

omit [SecondCountableTopology E] in
lemma toInit_eq (x : LinearMap.range (StrongDual.centeredToLp Œº 2)) {L : StrongDual ‚Ñù E}
    (hL : StrongDual.centeredToLp Œº 2 L = x) :
    toInit Œº x = ‚à´ y, L (y - ‚à´ z, z ‚àÇŒº) ‚Ä¢ (y - ‚à´ z, z ‚àÇŒº) ‚àÇŒº :=
  calc toInit Œº x
  _ = ‚à´ y, x.1 y ‚Ä¢ (y - ‚à´ z, z ‚àÇŒº) ‚àÇŒº := by
    rw [toInit]
    conv_rhs => rw [‚Üê (LinearMap.mem_range.mp x.2).choose_spec]
    refine integral_congr_ae ?_
    filter_upwards [StrongDual.centeredToLp_apply memLp_two_id (LinearMap.mem_range.mp x.2).choose]
      with y hy using by rw [hy]
  _ = ‚à´ y, StrongDual.centeredToLp Œº 2 L y ‚Ä¢ (y - ‚à´ z, z ‚àÇŒº) ‚àÇŒº := by rw [hL]
  _ = ‚à´ y, L (y - ‚à´ z, z ‚àÇŒº) ‚Ä¢ (y - ‚à´ z, z ‚àÇŒº) ‚àÇŒº := by
    refine integral_congr_ae ?_
    filter_upwards [StrongDual.centeredToLp_apply memLp_two_id L] with y hy using by rw [hy]

lemma apply_toInit (x : LinearMap.range (StrongDual.centeredToLp Œº 2)) (L : StrongDual ‚Ñù E) :
    L (toInit Œº x)
      = ‚à´ y, (LinearMap.mem_range.mp x.2).choose (y - ‚à´ z, z ‚àÇŒº) * L (y - ‚à´ z, z ‚àÇŒº) ‚àÇŒº := by
  rw [toInit, ‚Üê L.integral_comp_comm]
  ¬∑ simp
  rw [‚Üê integrable_norm_iff (by fun_prop)]
  simp only [map_sub, norm_smul]
  refine MemLp.integrable_mul (p := 2) (q := 2) ?_ ?_
  ¬∑ rw [memLp_norm_iff (by fun_prop)]
    exact (ContinuousLinearMap.memLp_two _).sub (memLp_const _)
  ¬∑ rw [memLp_norm_iff (by fun_prop)]
    exact MemLp.sub memLp_two_id (memLp_const _)

lemma apply_toInit_eq_inner (x : LinearMap.range (StrongDual.centeredToLp Œº 2))
    (L : StrongDual ‚Ñù E) :
    L (toInit Œº x) = ‚ü™StrongDual.centeredToLp Œº 2 L, x‚ü´_‚Ñù := by
  rw [‚Üê (LinearMap.mem_range.mp x.2).choose_spec, L2.inner_def, apply_toInit]
  simp only [RCLike.inner_apply, conj_trivial]
  refine integral_congr_ae ?_
  filter_upwards [StrongDual.centeredToLp_apply memLp_two_id L,
    StrongDual.centeredToLp_apply memLp_two_id (LinearMap.mem_range.mp x.2).choose]
    with y hy‚ÇÅ hy‚ÇÇ
  rw [hy‚ÇÅ, hy‚ÇÇ]

lemma norm_toInit_le (x : LinearMap.range (StrongDual.centeredToLp Œº 2)) :
    ‚ÄñtoInit Œº x‚Äñ ‚â§ ‚ÄñStrongDual.centeredToLp Œº 2‚Äñ * ‚Äñx‚Äñ := by
  refine norm_le_dual_bound ‚Ñù _ (by positivity) fun L ‚Ü¶ ?_
  calc ‚ÄñL (toInit Œº x)‚Äñ
  _ = ‚Äñ‚ü™StrongDual.centeredToLp Œº 2 L, x‚ü´_‚Ñù‚Äñ := by rw [apply_toInit_eq_inner]
  _ ‚â§ ‚ÄñStrongDual.centeredToLp Œº 2 L‚Äñ * ‚Äñx‚Äñ :=
    norm_inner_le_norm ((StrongDual.centeredToLp Œº 2) L) x
  _ ‚â§ ‚ÄñStrongDual.centeredToLp Œº 2‚Äñ * ‚ÄñL‚Äñ * ‚Äñx‚Äñ := by
    gcongr
    exact (StrongDual.centeredToLp Œº 2).le_opNorm L
  _ = ‚ÄñStrongDual.centeredToLp Œº 2‚Äñ * ‚Äñx‚Äñ * ‚ÄñL‚Äñ := by ring

end CameronMartinAux

open CameronMartinAux

/-- Continuous linear map from the Cameron-Martin space of a Gaussian measure to the space on
which that measure is defined. This map is injective: see `cmCoe_injective`.
Therefore, we can see the Cameron-Martin space as a subspace of the initial space, with a different
norm. -/
noncomputable
def cmCoe {Œº : Measure E} [HasTwoMoments Œº] : cameronMartin Œº ‚ÜíL[‚Ñù] E :=
  closureExtensionCLM (LinearMap.range (StrongDual.centeredToLp Œº 2)) <|
  LinearMap.mkContinuous
    { toFun x := toInit Œº x
      map_add' x y := by
        refine (eq_iff_forall_dual_eq (ùïú := ‚Ñù)).mpr fun L ‚Ü¶ ?_
        simp_rw [map_add, apply_toInit_eq_inner, Submodule.coe_add, inner_add_right]
      map_smul' r x := by
        refine (eq_iff_forall_dual_eq (ùïú := ‚Ñù)).mpr fun L ‚Ü¶ ?_
        simp_rw [map_smul, apply_toInit_eq_inner, Submodule.coe_smul, inner_smul_right]
        simp }
    ‚ÄñStrongDual.centeredToLp Œº 2‚Äñ norm_toInit_le

lemma apply_cmCoe_eq_inner (x : cameronMartin Œº) (L : StrongDual ‚Ñù E) :
    L (cmCoe x) = ‚ü™cmOfDual Œº L, x‚ü´_‚Ñù := by
  rw [cmOfDual_apply]
  unfold cameronMartin
  simp only [cmCoe, closureExtensionCLM_apply]
  revert x
  rw [‚Üê funext_iff]
  refine funext_topologicalClosure (by fun_prop) (by fun_prop) fun x ‚Ü¶ ?_
  rw [closureExtension_coe]
  swap; ¬∑ exact ContinuousLinearMap.uniformContinuous _
  simp only [LinearMap.mkContinuous_apply, LinearMap.coe_mk, AddHom.coe_mk]
  rw [apply_toInit_eq_inner]
  rfl

lemma eq_zero_of_cmCoe_eq_zero {x : cameronMartin Œº}
    (h : cmCoe x = 0) :
    x = 0 := by
  suffices ‚àÄ y : cameronMartin Œº, ‚ü™y, x‚ü´_‚Ñù = 0 by
    rw [‚Üê inner_self_eq_zero (ùïú := ‚Ñù) (x := x)]
    exact this x
  rw [‚Üê funext_iff]
  refine funext_topologicalClosure (by fun_prop) (by fun_prop) fun L ‚Ü¶ ?_
  obtain ‚ü®L', hL'‚ü© := LinearMap.mem_range.mp L.2
  have : cmOfDual Œº L' = L := by rw [cmOfDual_apply]; congr
  rw [‚Üê this, ‚Üê apply_cmCoe_eq_inner, h]
  simp

lemma cmCoe_injective : Function.Injective (cmCoe (Œº := Œº)) := by
  intro x y h
  rw [‚Üê sub_eq_zero, ‚Üê map_sub] at h
  rw [‚Üê sub_eq_zero, eq_zero_of_cmCoe_eq_zero h]

/-- Any point of the Cameron-Martin space has finite Cameron-Martin norm
`‚®Ü L (_ : Var[L; Œº] ‚â§ 1), L x` (when seen as a point of the initial space). -/
lemma apply_cmCoe_le_norm (x : cameronMartin Œº)
    {L : StrongDual ‚Ñù E} (hL : Var[L; Œº] ‚â§ 1) :
    L (cmCoe x) ‚â§ ‚Äñx‚Äñ := by
  calc L (cmCoe x)
  _ = ‚ü™cmOfDual Œº L, x‚ü´_‚Ñù := apply_cmCoe_eq_inner x L
  _ ‚â§ ‚ÄñcmOfDual Œº L‚Äñ * ‚Äñx‚Äñ := real_inner_le_norm (cmOfDual Œº L) x
  _ = ‚àöVar[L; Œº] * ‚Äñx‚Äñ := by rw [norm_cmOfDual]
  _ ‚â§ 1 * ‚Äñx‚Äñ := by gcongr; exact Real.sqrt_le_one.mpr hL
  _ = ‚Äñx‚Äñ := by rw [one_mul]

end CmCoe

variable [SecondCountableTopology E] [HasTwoMoments Œº]
  [Decidable (‚àÉ M, ‚àÄ L : StrongDual ‚Ñù E, Var[L; Œº] ‚â§ 1 ‚Üí L y ‚â§ M)]

@[simp]
lemma cmCoe_cmOfBounded (hy : ‚àÉ M, ‚àÄ L : StrongDual ‚Ñù E, Var[L; Œº] ‚â§ 1 ‚Üí L y ‚â§ M) :
    cmCoe (cmOfBounded Œº y) = y := by
  refine (eq_iff_forall_dual_eq (ùïú := ‚Ñù)).mpr fun L ‚Ü¶ ?_
  rw [‚Üê cmEval_cmOfDual hy, apply_cmCoe_eq_inner, cmEval_apply]

@[simp]
lemma cmOfBounded_cmCoe (x : cameronMartin Œº)
    [Decidable (‚àÉ M, ‚àÄ L : StrongDual ‚Ñù E, Var[L; Œº] ‚â§ 1 ‚Üí L (cmCoe x) ‚â§ M)] :
    cmOfBounded Œº (cmCoe x) = x := by
  refine cmCoe_injective ?_
  rw [cmCoe_cmOfBounded ‚ü®‚Äñx‚Äñ, fun _ hL ‚Ü¶ apply_cmCoe_le_norm x hL‚ü©]

lemma range_cmCoe :
    Set.range (cmCoe (Œº := Œº))
      = {y : E | ‚àÉ M, ‚àÄ L : StrongDual ‚Ñù E, Var[L; Œº] ‚â§ 1 ‚Üí L y ‚â§ M} := by
  ext y
  constructor
  ¬∑ rintro ‚ü®x, rfl‚ü©
    exact ‚ü®‚Äñx‚Äñ, fun L hL ‚Ü¶ apply_cmCoe_le_norm x hL‚ü©
  ¬∑ rintro hy
    classical
    refine ‚ü®cmOfBounded Œº y, ?_‚ü©
    rw [cmCoe_cmOfBounded hy]

lemma cmOfDual_inner_le_of_norm_cmOfDual_le (x : cameronMartin Œº) {L : StrongDual ‚Ñù E}
    (hL : ‚ÄñcmOfDual Œº L‚Äñ ‚â§ 1) :
    ‚ü™cmOfDual Œº L, x‚ü´_‚Ñù ‚â§ ‚®Ü (L : StrongDual ‚Ñù E) (_ : Var[L; Œº] ‚â§ 1), L (cmCoe x) := by
  refine le_ciSup_of_le ?_ L ?_
  ¬∑ refine ‚ü®‚Äñx‚Äñ, ?_‚ü©
    simp only [mem_upperBounds, Set.mem_range, forall_exists_index, forall_apply_eq_imp_iff]
    intro L
    by_cases hL : Var[L; Œº] ‚â§ 1
    ¬∑ simpa [hL] using apply_cmCoe_le_norm x hL
    ¬∑ simp [hL]
  have hL' : Var[L; Œº] ‚â§ 1 := by rwa [norm_cmOfDual, Real.sqrt_le_one] at hL
  simp only [hL', ciSup_unique]
  rw [‚Üê apply_cmCoe_eq_inner]

lemma cmOfDual_inner_le_mul (x : cameronMartin Œº) (L : StrongDual ‚Ñù E) :
    ‚ü™cmOfDual Œº L, x‚ü´_‚Ñù
      ‚â§ ‚ÄñcmOfDual Œº L‚Äñ * ‚®Ü (L : StrongDual ‚Ñù E) (_ : Var[L; Œº] ‚â§ 1), L (cmCoe x) := by
  by_cases h_zero : ‚ÄñcmOfDual Œº L‚Äñ = 0
  ¬∑ simp only [h_zero, zero_mul]
    rw [inner_eq_zero_of_left _ h_zero]
  have h := cmOfDual_inner_le_of_norm_cmOfDual_le x (L := ‚ÄñcmOfDual Œº L‚Äñ‚Åª¬π ‚Ä¢ L) ?_
  ¬∑ simp only [map_smul, inner_smul_left, map_inv‚ÇÄ, conj_trivial] at h
    rwa [inv_mul_le_iff‚ÇÄ (by positivity)] at h
  ¬∑ simp only [map_smul, norm_smul, norm_inv, norm_norm]
    exact inv_mul_le_one

lemma inner_cameronMartin_le_mul_ciSup (x y : cameronMartin Œº) :
    ‚ü™y, x‚ü´_‚Ñù ‚â§ ‚Äñy‚Äñ * ‚®Ü (L : StrongDual ‚Ñù E) (_ : Var[L; Œº] ‚â§ 1), L (cmCoe x) := by
  induction y using induction_topologicalClosure with
  | hp =>
    exact isClosed_le (by fun_prop) (by fun_prop)
  | ih a =>
    obtain ‚ü®L, hL‚ü© := LinearMap.mem_range.mp a.2
    have : (a : cameronMartin Œº) = cmOfDual Œº L := by
      simp_rw [cmOfDual_apply, hL]
    rw [this]
    exact cmOfDual_inner_le_mul x L

lemma norm_cameronMartin_eq_ciSup (x : cameronMartin Œº) :
    ‚Äñx‚Äñ = ‚®Ü (L : StrongDual ‚Ñù E) (_ : Var[L; Œº] ‚â§ 1), L (cmCoe x) := by
  refine le_antisymm ?_ ?_
  ¬∑ refine InnerProductSpace.norm_le_dual_bound x ?_ fun y ‚Ü¶ ?_
    ¬∑ by_cases h_bdd :
        BddAbove (Set.range fun L : StrongDual ‚Ñù E ‚Ü¶ ‚®Ü (_ : Var[L; Œº] ‚â§ 1), L (cmCoe x))
      ¬∑ exact le_ciSup_of_le h_bdd 0 (by simp)
      ¬∑ simp [h_bdd]
    rw [real_inner_comm, mul_comm]
    exact inner_cameronMartin_le_mul_ciSup x y
  ¬∑ refine ciSup_le fun L ‚Ü¶ ?_
    by_cases hL : Var[L; Œº] ‚â§ 1
    ¬∑ simpa [hL] using apply_cmCoe_le_norm x hL
    ¬∑ simp [hL]

lemma norm_cmOfBounded {y : E} [Decidable (‚àÉ M, ‚àÄ L : StrongDual ‚Ñù E, Var[L; Œº] ‚â§ 1 ‚Üí L y ‚â§ M)]
    (hy : ‚àÉ M, ‚àÄ L : StrongDual ‚Ñù E, Var[L; Œº] ‚â§ 1 ‚Üí L y ‚â§ M) :
    ‚ÄñcmOfBounded Œº y‚Äñ = ‚®Ü (L : StrongDual ‚Ñù E) (_ : Var[L; Œº] ‚â§ 1), L y := by
  rw [norm_cameronMartin_eq_ciSup]
  simp [cmCoe_cmOfBounded hy]

end ProbabilityTheory
