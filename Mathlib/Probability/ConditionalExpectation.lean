/-
Copyright (c) 2022 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import Mathlib.Probability.Notation
import Mathlib.Probability.Independence.Basic
import Mathlib.MeasureTheory.Function.ConditionalExpectation.Basic

/-!

# Probabilistic properties of the conditional expectation

This file contains some properties about the conditional expectation which does not belong in
the main conditional expectation file.

## Main result

* `MeasureTheory.condExp_indep_eq`: If `mâ‚, mâ‚‚` are independent Ïƒ-algebras and `f` is an
  `mâ‚`-measurable function, then `ğ”¼[f | mâ‚‚] = ğ”¼[f]` almost everywhere.

-/


open TopologicalSpace Filter

open scoped NNReal ENNReal MeasureTheory ProbabilityTheory

namespace MeasureTheory

open ProbabilityTheory

variable {Î© E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [CompleteSpace E]
  {mâ‚ mâ‚‚ m : MeasurableSpace Î©} {Î¼ : Measure Î©} {f : Î© â†’ E}

/-- If `mâ‚, mâ‚‚` are independent Ïƒ-algebras and `f` is `mâ‚`-measurable, then `ğ”¼[f | mâ‚‚] = ğ”¼[f]`
almost everywhere. -/
theorem condExp_indep_eq (hleâ‚ : mâ‚ â‰¤ m) (hleâ‚‚ : mâ‚‚ â‰¤ m) [SigmaFinite (Î¼.trim hleâ‚‚)]
    (hf : StronglyMeasurable[mâ‚] f) (hindp : Indep mâ‚ mâ‚‚ Î¼) : Î¼[f|mâ‚‚] =áµ[Î¼] fun _ => Î¼[f] := by
  by_cases hfint : Integrable f Î¼
  swap; Â· rw [condExp_of_not_integrable hfint, integral_undef hfint]; rfl
  refine (ae_eq_condExp_of_forall_setIntegral_eq hleâ‚‚ hfint
    (fun s _ hs â†¦ integrableOn_const hs.ne) (fun s hms hs => ?_)
      stronglyMeasurable_const.aestronglyMeasurable).symm
  rw [setIntegral_const]
  rw [â† memLp_one_iff_integrable] at hfint
  refine MemLp.induction_stronglyMeasurable hleâ‚ ENNReal.one_ne_top _ ?_ ?_ ?_ ?_ hfint ?_
  Â· exact âŸ¨f, hf, EventuallyEq.rflâŸ©
  Â· intro c t hmt _
    rw [Indep_iff] at hindp
    rw [integral_indicator (hleâ‚ _ hmt), setIntegral_const, smul_smul, measureReal_def,
      measureReal_def, â† ENNReal.toReal_mul,
      mul_comm, â† hindp _ _ hmt hms, setIntegral_indicator (hleâ‚ _ hmt), setIntegral_const,
      Set.inter_comm, measureReal_def]
  Â· intro u v _ huint hvint hu hv hu_eq hv_eq
    rw [memLp_one_iff_integrable] at huint hvint
    rw [integral_add' huint hvint, smul_add, hu_eq, hv_eq,
      integral_add' huint.integrableOn hvint.integrableOn]
  Â· have heqâ‚ : (fun f : lpMeas E â„ mâ‚ 1 Î¼ => âˆ« x, (f : Î© â†’ E) x âˆ‚Î¼) =
        (fun f : Lp E 1 Î¼ => âˆ« x, f x âˆ‚Î¼) âˆ˜ Submodule.subtypeL _ := by
      refine funext fun f => integral_congr_ae ?_
      simp_rw [Submodule.coe_subtypeL', Submodule.coe_subtype]; norm_cast
    have heqâ‚‚ : (fun f : lpMeas E â„ mâ‚ 1 Î¼ => âˆ« x in s, (f : Î© â†’ E) x âˆ‚Î¼) =
        (fun f : Lp E 1 Î¼ => âˆ« x in s, f x âˆ‚Î¼) âˆ˜ Submodule.subtypeL _ := by
      refine funext fun f => integral_congr_ae (ae_restrict_of_ae ?_)
      simp_rw [Submodule.coe_subtypeL', Submodule.coe_subtype]
      exact Eventually.of_forall fun _ => (by trivial)
    refine isClosed_eq (Continuous.const_smul ?_ _) ?_
    Â· rw [heqâ‚]
      exact continuous_integral.comp (ContinuousLinearMap.continuous _)
    Â· rw [heqâ‚‚]
      exact (continuous_setIntegral _).comp (ContinuousLinearMap.continuous _)
  Â· intro u v huv _ hueq
    rwa [â† integral_congr_ae huv, â†
      (setIntegral_congr_ae (hleâ‚‚ _ hms) _ : âˆ« x in s, u x âˆ‚Î¼ = âˆ« x in s, v x âˆ‚Î¼)]
    filter_upwards [huv] with x hx _ using hx

end MeasureTheory
