/-
Copyright (c) 2025 Etienne Marion. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Etienne Marion
-/
import Mathlib.MeasureTheory.Constructions.Cylinders
import Mathlib.Probability.Independence.Basic

/-!
# Independence of stochastic processes

We prove that a stochastic process $(X_s)_{s \in S}$ is independent from a random variable $Y$ if
for all $s_1, ..., s_p \in S$ the family $(X_{s_1}, ..., X_{s_p})$ is independent from $Y$.

We prove that two stochastic processes $(X_s)_{s \in S}$ and $(Y_t)_{t \in T}$ are independent if
for all $s_1, ..., s_p \in S$ and $t_1, ..., t_q \in T$ the two families
$(X_{s_1}, ..., X_{s_p})$ and $(Y_{t_1}, ..., Y_{t_q})$ are independent.
We prove an analogous condition for a family of stochastic processes.

## Tags

independence, stochastic processes
-/

open MeasureTheory MeasurableSpace

namespace ProbabilityTheory

variable {S Î© : Type*} {mÎ© : MeasurableSpace Î©}

namespace Kernel

variable {Î± : Type*} {mÎ± : MeasurableSpace Î±} {Îº : Kernel Î± Î©} {P : Measure Î±}

/-- A stochastic process $(X_s)_{s \in S}$ is independent from a random variable $Y$ if
for all $s_1, ..., s_p \in S$ the family $(X_{s_1}, ..., X_{s_p})$ is independent from $Y$. -/
lemma IndepFun.process_indepFun {ğ“§ : S â†’ Type*} {ğ“¨ : Type*}
    [âˆ€ i, MeasurableSpace (ğ“§ i)] [MeasurableSpace ğ“¨] {X : (i : S) â†’ Î© â†’ ğ“§ i}
    {Y : Î© â†’ ğ“¨} (hX : âˆ€ i, Measurable (X i)) (hY : Measurable Y)
    (h : âˆ€ (I : Finset S),
      IndepFun (fun Ï‰ (i : I) â†¦ X i Ï‰) Y Îº P) [IsZeroOrMarkovKernel Îº] :
    IndepFun (fun Ï‰ i â†¦ X i Ï‰) Y Îº P := by
  -- The Ï€-system obtained by pulling back the Ï€-system of square cylinders by `X`.
  let Ï€X := {s : Set Î© | âˆƒ t âˆˆ squareCylinders (fun i â†¦ {s : Set (ğ“§ i) | MeasurableSet s}),
      (fun Ï‰ i â†¦ X i Ï‰) â»Â¹' t = s}
  have Ï€X_pi : IsPiSystem Ï€X :=
    IsPiSystem.comap (isPiSystem_squareCylinders (fun _ â†¦ isPiSystem_measurableSet) (by simp)) _
  have Ï€X_gen : (MeasurableSpace.pi.comap fun Ï‰ i â†¦ X i Ï‰) = generateFrom Ï€X := by
    rw [generateFrom_squareCylinders.symm, MeasurableSpace.comap_generateFrom]
    rfl
  -- To prove independence, we prove independence of the generating Ï€-system with the `Ïƒ`-alebra.
  refine IndepSets.indep (measurable_pi_iff.2 hX).comap_le hY.comap_le
    Ï€X_pi (@isPiSystem_measurableSet Î© (.comap Y inferInstance)) Ï€X_gen
    (@generateFrom_measurableSet Î© (.comap Y inferInstance)).symm ?_
  rintro - - âŸ¨-, âŸ¨I, s, hs, rflâŸ©, rflâŸ© âŸ¨t, ht, rflâŸ©
  simp only [Set.mem_pi, Set.mem_univ, Set.mem_setOf_eq, forall_const] at hs
  have : (fun Ï‰ i â†¦ X i Ï‰) â»Â¹' .pi I s =
      (fun Ï‰ (i : I) â†¦ X i Ï‰) â»Â¹' .pi (SetLike.coe Finset.univ) (fun i â†¦ s i)
       := by
    ext; simp
  have h1 : MeasurableSet <| .pi (SetLike.coe Finset.univ) (fun (i : I) â†¦ s i) :=
    .pi (Finset.countable_toSet _) (fun _ _ â†¦ hs _)
  filter_upwards [(h I).measure_inter_preimage_eq_mul _ _ h1 ht] with Ï‰ hÏ‰
  rw [this, hÏ‰]

/-- A random variable $X$ is independent from a stochastic process $(Y_s)_{s \in S}$  if
for all $s_1, ..., s_p \in S$ the variable $Y$ is independent from the family
$(X_{s_1}, ..., X_{s_p})$. -/
lemma IndepFun.indepFun_process {ğ“§ : Type*} {ğ“¨ : S â†’ Type*}
    [MeasurableSpace ğ“§] [âˆ€ i, MeasurableSpace (ğ“¨ i)] {X : Î© â†’ ğ“§}
    {Y : (i : S) â†’ Î© â†’ ğ“¨ i} (hX : Measurable X) (hY : âˆ€ i, Measurable (Y i))
    (h : âˆ€ (I : Finset S),
      IndepFun X (fun Ï‰ (i : I) â†¦ Y i Ï‰) Îº P) [IsZeroOrMarkovKernel Îº] :
    IndepFun X (fun Ï‰ i â†¦ Y i Ï‰) Îº P :=
  (IndepFun.process_indepFun hY hX (fun I â†¦ (h I).symm)).symm

/-- Two stochastic processes $(X_s)_{s \in S}$ and $(Y_t)_{t \in T}$ are independent if
for all $s_1, ..., s_p \in S$ and $t_1, ..., t_q \in T$ the two families
$(X_{s_1}, ..., X_{s_p})$ and $(Y_{t_1}, ..., Y_{t_q})$ are independent. -/
lemma IndepFun.process_indepFun_process {T : Type*} {ğ“§ : S â†’ Type*} {ğ“¨ : T â†’ Type*}
    [âˆ€ i, MeasurableSpace (ğ“§ i)] [âˆ€ j, MeasurableSpace (ğ“¨ j)] {X : (i : S) â†’ Î© â†’ ğ“§ i}
    {Y : (j : T) â†’ Î© â†’ ğ“¨ j} (hX : âˆ€ i, Measurable (X i)) (hY : âˆ€ j, Measurable (Y j))
    (h : âˆ€ (I : Finset S) (J : Finset T),
      IndepFun (fun Ï‰ (i : I) â†¦ X i Ï‰) (fun Ï‰ (j : J) â†¦ Y j Ï‰) Îº P) [IsZeroOrMarkovKernel Îº] :
    IndepFun (fun Ï‰ i â†¦ X i Ï‰) (fun Ï‰ j â†¦ Y j Ï‰) Îº P := by
  refine IndepFun.process_indepFun hX (measurable_pi_lambda _ hY) fun I â†¦ ?_
  exact IndepFun.indepFun_process (measurable_pi_lambda _ fun _ â†¦ hX _) hY fun J â†¦ h I J

/-- Stochastic processes $((X^s_t)_{t \in T_s})_{s \in S}$ are mutually independent if
for all $s_1, ..., s_n$ and all $t^{s_i}_1, ..., t^{s_i}_{p_i}^ the families
$(X^{s_1}_{t^{s_1}_1}, ..., X^{s_1}_{t^{s_1}_{p_1}}), ...,
(X^{s_n}_{t^{s_n}_1}, ..., X^{s_n}_{t^{s_n}_{p_n}})$ are mutually independent. -/
lemma iIndepFun.iIndepFun_process {T : S â†’ Type*} {ğ“§ : (i : S) â†’ (j : T i) â†’ Type*}
    [âˆ€ i j, MeasurableSpace (ğ“§ i j)] {X : (i : S) â†’ (j : T i) â†’ Î© â†’ ğ“§ i j}
    (hX : âˆ€ i j, Measurable (X i j))
    (h : âˆ€ (I : Finset S) (J : (i : I) â†’ Finset (T i)),
      iIndepFun (fun i Ï‰ (j : J i) â†¦ X i j Ï‰) Îº P) :
    iIndepFun (fun i Ï‰ j â†¦ X i j Ï‰) Îº P := by
  obtain rfl | hÎ¼ := eq_or_ne P 0
  Â· simp
  obtain âŸ¨Î·, Î·_eq, hÎ·âŸ© : âˆƒ (Î· : Kernel Î± Î©), Îº =áµ[P] Î· âˆ§ IsMarkovKernel Î· :=
    exists_ae_eq_isMarkovKernel (h âˆ… fun _ â†¦ âˆ…).ae_isProbabilityMeasure hÎ¼
  apply iIndepFun.congr (Filter.EventuallyEq.symm Î·_eq)
  let Ï€ i := {s : Set Î© | âˆƒ t âˆˆ squareCylinders (fun j â†¦ {s : Set (ğ“§ i j) | MeasurableSet s}),
    (fun Ï‰ j â†¦ X i j Ï‰) â»Â¹' t = s}
  have Ï€_pi i : IsPiSystem (Ï€ i) :=
    (isPiSystem_squareCylinders (fun _ â†¦ isPiSystem_measurableSet) (by simp)).comap _
  have Ï€_gen i : (MeasurableSpace.pi.comap fun Ï‰ j â†¦ X i j Ï‰) = generateFrom (Ï€ i) := by
    rw [generateFrom_squareCylinders.symm, MeasurableSpace.comap_generateFrom]
    rfl
  refine iIndepSets.iIndep _ (fun i â†¦ (measurable_pi_iff.2 (hX i)).comap_le) Ï€ Ï€_pi Ï€_gen
    fun I s hs â†¦ ?_
  simp only [squareCylinders, Set.mem_pi, Set.mem_univ, Set.mem_setOf_eq, forall_const,
    â†“existsAndEq, and_true, Ï€] at hs
  choose! J t ht hs using hs
  simp_rw [Set.iInterâ‚‚_congr (fun i hi â†¦ (hs i hi).symm),
    I.prod_congr rfl (fun i hi â†¦ congrArg _ (hs i hi).symm)]
  have : (â‹‚ i âˆˆ I, (fun Ï‰ j â†¦ X i j Ï‰) â»Â¹' .pi (J i) (t i)) =
      (â‹‚ i âˆˆ (.univ : Finset I), (fun Ï‰ (j : J i) â†¦ X i j Ï‰) â»Â¹'
        .pi (SetLike.coe Finset.univ) (fun j â†¦ t i j)) := by
    ext; simp
  have h' (i : I) (hi : i âˆˆ Finset.univ) :
      MeasurableSet <| (SetLike.coe Finset.univ).pi fun (j : J i) â†¦ t i j :=
    .pi (Finset.countable_toSet _) (fun _ _ â†¦ ht _ i.2 _)
  filter_upwards [(h I (fun i â†¦ J i)).measure_inter_preimage_eq_mul _ _ .univ h',
    Î·_eq] with Ï‰ hÏ‰ hÎ·
  rw [this, â† hÎ·, hÏ‰, â† I.prod_coe_sort]
  congrm âˆ _, Îº Ï‰ ?_
  ext; simp

end Kernel

variable {P : Measure Î©}

/-- A stochastic process $(X_s)_{s \in S}$ is independent from a random variable $Y$ if
for all $s_1, ..., s_p \in S$ the family $(X_{s_1}, ..., X_{s_p})$ is independent from $Y$. -/
lemma IndepFun.process_indepFun {ğ“§ : S â†’ Type*} {ğ“¨ : Type*}
    [âˆ€ i, MeasurableSpace (ğ“§ i)] [MeasurableSpace ğ“¨] {X : (i : S) â†’ Î© â†’ ğ“§ i}
    {Y : Î© â†’ ğ“¨} (hX : âˆ€ i, Measurable (X i)) (hY : AEMeasurable Y P)
    (h : âˆ€ (I : Finset S),
      IndepFun (fun Ï‰ (i : I) â†¦ X i Ï‰) Y P) [IsZeroOrProbabilityMeasure P] :
    IndepFun (fun Ï‰ i â†¦ X i Ï‰) Y P := by
  suffices (fun Ï‰ i â†¦ X i Ï‰) âŸ‚áµ¢[P] (hY.mk Y) from
    this.congr .rfl hY.ae_eq_mk.symm
  exact Kernel.IndepFun.process_indepFun hX hY.measurable_mk (fun I â†¦ (h I).congr .rfl hY.ae_eq_mk)

/-- A random variable $X$ is independent from a stochastic process $(Y_s)_{s \in S}$  if
for all $s_1, ..., s_p \in S$ the variable $Y$ is independent from the family
$(X_{s_1}, ..., X_{s_p})$. -/
lemma IndepFun.indepFun_process {ğ“§ : Type*} {ğ“¨ : S â†’ Type*}
    [MeasurableSpace ğ“§] [âˆ€ i, MeasurableSpace (ğ“¨ i)] {X : Î© â†’ ğ“§}
    {Y : (i : S) â†’ Î© â†’ ğ“¨ i} (hX : AEMeasurable X P) (hY : âˆ€ i, Measurable (Y i))
    (h : âˆ€ (I : Finset S),
      IndepFun X (fun Ï‰ (i : I) â†¦ Y i Ï‰) P) [IsZeroOrProbabilityMeasure P] :
    IndepFun X (fun Ï‰ i â†¦ Y i Ï‰) P := by
  suffices (hX.mk X) âŸ‚áµ¢[P] (fun Ï‰ i â†¦ Y i Ï‰) from
    this.congr hX.ae_eq_mk.symm .rfl
  exact Kernel.IndepFun.indepFun_process hX.measurable_mk hY (fun I â†¦ (h I).congr hX.ae_eq_mk .rfl)

/-- Two stochastic processes $(X_s)_{s \in S}$ and $(Y_t)_{t \in T}$ are independent if
for all $s_1, ..., s_p \in S$ and $t_1, ..., t_q \in T$ the two families
$(X_{s_1}, ..., X_{s_p})$ and $(Y_{t_1}, ..., Y_{t_q})$ are independent. -/
lemma IndepFun.process_indepFun_process {T : Type*} {ğ“§ : S â†’ Type*} {ğ“¨ : T â†’ Type*}
    [âˆ€ i, MeasurableSpace (ğ“§ i)] [âˆ€ j, MeasurableSpace (ğ“¨ j)] {X : (i : S) â†’ Î© â†’ ğ“§ i}
    {Y : (j : T) â†’ Î© â†’ ğ“¨ j} (hX : âˆ€ i, Measurable (X i)) (hY : âˆ€ j, Measurable (Y j))
    (h : âˆ€ (I : Finset S) (J : Finset T),
      IndepFun (fun Ï‰ (i : I) â†¦ X i Ï‰) (fun Ï‰ (j : J) â†¦ Y j Ï‰) P) [IsZeroOrProbabilityMeasure P] :
    IndepFun (fun Ï‰ i â†¦ X i Ï‰) (fun Ï‰ j â†¦ Y j Ï‰) P :=
  Kernel.IndepFun.process_indepFun_process hX hY h

/-- Stochastic processes $((X^s_t)_{t \in T_s})_{s \in S}$ are mutually independent if
for all $s_1, ..., s_n$ and all $t^{s_i}_1, ..., t^{s_i}_{p_i}^ the families
$(X^{s_1}_{t^{s_1}_1}, ..., X^{s_1}_{t^{s_1}_{p_1}}), ...,
(X^{s_n}_{t^{s_n}_1}, ..., X^{s_n}_{t^{s_n}_{p_n}})$ are mutually independent. -/
lemma iIndepFun.iIndepFun_process {T : S â†’ Type*} {ğ“§ : (i : S) â†’ (j : T i) â†’ Type*}
    [âˆ€ i j, MeasurableSpace (ğ“§ i j)] {X : (i : S) â†’ (j : T i) â†’ Î© â†’ ğ“§ i j}
    (hX : âˆ€ i j, Measurable (X i j))
    (h : âˆ€ (I : Finset S) (J : (i : I) â†’ Finset (T i)), iIndepFun (fun i Ï‰ (j : J i) â†¦ X i j Ï‰) P) :
    iIndepFun (fun i Ï‰ j â†¦ X i j Ï‰) P :=
  Kernel.iIndepFun.iIndepFun_process hX h

end ProbabilityTheory
