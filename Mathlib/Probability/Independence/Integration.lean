/-
Copyright (c) 2021 Martin Zinkevich. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Martin Zinkevich, Vincent Beffara, Etienne Marion
-/
import Mathlib.MeasureTheory.Integral.Pi
import Mathlib.Probability.Independence.Integrable
import Mathlib.Probability.Notation

/-!
# Integration in Probability Theory

Integration results for independent random variables. Specifically, for two
independent random variables X and Y over the extended non-negative
reals, `E[X * Y] = E[X] * E[Y]`, and similar results.

## Implementation notes

Many lemmas in this file take two arguments of the same typeclass. It is worth remembering that lean
will always pick the later typeclass in this situation, and does not care whether the arguments are
`[]`, `{}`, or `()`. All of these use the `MeasurableSpace` `M2` to define `Î¼`:

```lean
example {M1 : MeasurableSpace Î©} [M2 : MeasurableSpace Î©] {Î¼ : Measure Î©} : sorry := sorry
example [M1 : MeasurableSpace Î©] {M2 : MeasurableSpace Î©} {Î¼ : Measure Î©} : sorry := sorry
```

-/


open Set MeasureTheory

open scoped ENNReal MeasureTheory

variable {Î© ğ•œ : Type*} [RCLike ğ•œ] {mÎ© : MeasurableSpace Î©} {Î¼ : Measure Î©} {f g : Î© â†’ â„â‰¥0âˆ}
    {X Y : Î© â†’ ğ•œ}

namespace ProbabilityTheory

/-- If a random variable `f` in `â„â‰¥0âˆ` is independent of an event `T`, then if you restrict the
  random variable to `T`, then `E[f * indicator T c 0]=E[f] * E[indicator T c 0]`. It is useful for
  `lintegral_mul_eq_lintegral_mul_lintegral_of_independent_measurableSpace`. -/
theorem lintegral_mul_indicator_eq_lintegral_mul_lintegral_indicator {Mf mÎ© : MeasurableSpace Î©}
    {Î¼ : Measure Î©} (hMf : Mf â‰¤ mÎ©) (c : â„â‰¥0âˆ) {T : Set Î©} (h_meas_T : MeasurableSet T)
    (h_ind : IndepSets {s | MeasurableSet[Mf] s} {T} Î¼) (h_meas_f : Measurable[Mf] f) :
    (âˆ«â» Ï‰, f Ï‰ * T.indicator (fun _ => c) Ï‰ âˆ‚Î¼) =
      (âˆ«â» Ï‰, f Ï‰ âˆ‚Î¼) * âˆ«â» Ï‰, T.indicator (fun _ => c) Ï‰ âˆ‚Î¼ := by
  revert f
  have h_mul_indicator : âˆ€ g, Measurable g â†’ Measurable fun a => g a * T.indicator (fun _ => c) a :=
    fun g h_mg => h_mg.mul (measurable_const.indicator h_meas_T)
  apply @Measurable.ennreal_induction _ Mf
  Â· intro c' s' h_meas_s'
    simp_rw [â† inter_indicator_mul]
    rw [lintegral_indicator (MeasurableSet.inter (hMf _ h_meas_s') h_meas_T),
      lintegral_indicator (hMf _ h_meas_s'), lintegral_indicator h_meas_T]
    simp only [lintegral_const, univ_inter,
      MeasurableSet.univ, Measure.restrict_apply]
    rw [IndepSets_iff] at h_ind
    rw [mul_mul_mul_comm, h_ind s' T h_meas_s' (Set.mem_singleton _)]
  Â· intro f' g _ h_meas_f' _ h_ind_f' h_ind_g
    have h_measM_f' : Measurable f' := h_meas_f'.mono hMf le_rfl
    simp_rw [Pi.add_apply, right_distrib]
    rw [lintegral_add_left (h_mul_indicator _ h_measM_f'), lintegral_add_left h_measM_f',
      right_distrib, h_ind_f', h_ind_g]
  Â· intro f h_meas_f h_mono_f h_ind_f
    have h_measM_f : âˆ€ n, Measurable (f n) := fun n => (h_meas_f n).mono hMf le_rfl
    simp_rw [ENNReal.iSup_mul]
    rw [lintegral_iSup h_measM_f h_mono_f, lintegral_iSup, ENNReal.iSup_mul]
    Â· simp_rw [â† h_ind_f]
    Â· exact fun n => h_mul_indicator _ (h_measM_f n)
    Â· exact fun m n h_le a => mul_le_mul_right' (h_mono_f h_le a) _

/--
If `f` and `g` are independent random variables with values in `â„â‰¥0âˆ`,
then `E[f * g] = E[f] * E[g]`. However, instead of directly using the independence
of the random variables, it uses the independence of measurable spaces for the
domains of `f` and `g`. This is similar to the sigma-algebra approach to
independence. See `lintegral_mul_eq_lintegral_mul_lintegral_of_indepFun` for
a more common variant of the product of independent variables. -/
theorem lintegral_mul_eq_lintegral_mul_lintegral_of_independent_measurableSpace
    {Mf Mg mÎ© : MeasurableSpace Î©} {Î¼ : Measure Î©} (hMf : Mf â‰¤ mÎ©) (hMg : Mg â‰¤ mÎ©)
    (h_ind : Indep Mf Mg Î¼) (h_meas_f : Measurable[Mf] f) (h_meas_g : Measurable[Mg] g) :
    âˆ«â» Ï‰, f Ï‰ * g Ï‰ âˆ‚Î¼ = (âˆ«â» Ï‰, f Ï‰ âˆ‚Î¼) * âˆ«â» Ï‰, g Ï‰ âˆ‚Î¼ := by
  revert g
  have h_measM_f : Measurable f := h_meas_f.mono hMf le_rfl
  apply @Measurable.ennreal_induction _ Mg
  Â· intro c s h_s
    apply lintegral_mul_indicator_eq_lintegral_mul_lintegral_indicator hMf _ (hMg _ h_s) _ h_meas_f
    apply indepSets_of_indepSets_of_le_right h_ind
    rwa [singleton_subset_iff]
  Â· intro f' g _ h_measMg_f' _ h_ind_f' h_ind_g'
    have h_measM_f' : Measurable f' := h_measMg_f'.mono hMg le_rfl
    simp_rw [Pi.add_apply, left_distrib]
    rw [lintegral_add_left h_measM_f', lintegral_add_left (h_measM_f.mul h_measM_f'), left_distrib,
      h_ind_f', h_ind_g']
  Â· intro f' h_meas_f' h_mono_f' h_ind_f'
    have h_measM_f' : âˆ€ n, Measurable (f' n) := fun n => (h_meas_f' n).mono hMg le_rfl
    simp_rw [ENNReal.mul_iSup]
    rw [lintegral_iSup, lintegral_iSup h_measM_f' h_mono_f', ENNReal.mul_iSup]
    Â· simp_rw [â† h_ind_f']
    Â· exact fun n => h_measM_f.mul (h_measM_f' n)
    Â· exact fun n m (h_le : n â‰¤ m) a => mul_le_mul_left' (h_mono_f' h_le a) _

/-- If `f` and `g` are independent random variables with values in `â„â‰¥0âˆ`,
then `E[f * g] = E[f] * E[g]`. -/
theorem lintegral_mul_eq_lintegral_mul_lintegral_of_indepFun (h_meas_f : Measurable f)
    (h_meas_g : Measurable g) (h_indep_fun : IndepFun f g Î¼) :
    (âˆ«â» Ï‰, (f * g) Ï‰ âˆ‚Î¼) = (âˆ«â» Ï‰, f Ï‰ âˆ‚Î¼) * âˆ«â» Ï‰, g Ï‰ âˆ‚Î¼ :=
  lintegral_mul_eq_lintegral_mul_lintegral_of_independent_measurableSpace
    (measurable_iff_comap_le.1 h_meas_f) (measurable_iff_comap_le.1 h_meas_g) h_indep_fun
    (Measurable.of_comap_le le_rfl) (Measurable.of_comap_le le_rfl)

/-- If `f` and `g` with values in `â„â‰¥0âˆ` are independent and almost everywhere measurable,
then `E[f * g] = E[f] * E[g]` (slightly generalizing
`lintegral_mul_eq_lintegral_mul_lintegral_of_indepFun`). -/
theorem lintegral_mul_eq_lintegral_mul_lintegral_of_indepFun' (h_meas_f : AEMeasurable f Î¼)
    (h_meas_g : AEMeasurable g Î¼) (h_indep_fun : IndepFun f g Î¼) :
    (âˆ«â» Ï‰, (f * g) Ï‰ âˆ‚Î¼) = (âˆ«â» Ï‰, f Ï‰ âˆ‚Î¼) * âˆ«â» Ï‰, g Ï‰ âˆ‚Î¼ := by
  have fg_ae : f * g =áµ[Î¼] h_meas_f.mk _ * h_meas_g.mk _ := h_meas_f.ae_eq_mk.mul h_meas_g.ae_eq_mk
  rw [lintegral_congr_ae h_meas_f.ae_eq_mk, lintegral_congr_ae h_meas_g.ae_eq_mk,
    lintegral_congr_ae fg_ae]
  apply lintegral_mul_eq_lintegral_mul_lintegral_of_indepFun h_meas_f.measurable_mk
      h_meas_g.measurable_mk
  exact h_indep_fun.congr h_meas_f.ae_eq_mk h_meas_g.ae_eq_mk

theorem lintegral_mul_eq_lintegral_mul_lintegral_of_indepFun'' (h_meas_f : AEMeasurable f Î¼)
    (h_meas_g : AEMeasurable g Î¼) (h_indep_fun : IndepFun f g Î¼) :
    âˆ«â» Ï‰, f Ï‰ * g Ï‰ âˆ‚Î¼ = (âˆ«â» Ï‰, f Ï‰ âˆ‚Î¼) * âˆ«â» Ï‰, g Ï‰ âˆ‚Î¼ :=
  lintegral_mul_eq_lintegral_mul_lintegral_of_indepFun' h_meas_f h_meas_g h_indep_fun

theorem lintegral_prod_eq_prod_lintegral_of_indepFun {Î¹ : Type*}
    (s : Finset Î¹) (X : Î¹ â†’ Î© â†’ â„â‰¥0âˆ) (hX : iIndepFun X Î¼)
    (x_mea : âˆ€ i, Measurable (X i)) :
    âˆ«â» Ï‰, âˆ i âˆˆ s, (X i Ï‰) âˆ‚Î¼ = âˆ i âˆˆ s, âˆ«â» Ï‰, X i Ï‰ âˆ‚Î¼ := by
  have : IsProbabilityMeasure Î¼ := hX.isProbabilityMeasure
  induction s using Finset.cons_induction with
  | empty => simp only [Finset.prod_empty, lintegral_const, measure_univ, mul_one]
  | cons j s hj ihs =>
    simp only [â† Finset.prod_apply, Finset.prod_cons, â† ihs]
    apply lintegral_mul_eq_lintegral_mul_lintegral_of_indepFun'
    Â· exact (x_mea j).aemeasurable
    Â· exact s.aemeasurable_prod (fun i _ â†¦ (x_mea i).aemeasurable)
    Â· exact (iIndepFun.indepFun_finset_prod_of_notMem hX x_mea hj).symm

/-- The product of two independent, integrable, real-valued random variables is integrable. -/
theorem IndepFun.integrable_mul {Î² : Type*} [MeasurableSpace Î²] {X Y : Î© â†’ Î²}
    [NormedDivisionRing Î²] [BorelSpace Î²] (hXY : IndepFun X Y Î¼) (hX : Integrable X Î¼)
    (hY : Integrable Y Î¼) : Integrable (X * Y) Î¼ := by
  let nX : Î© â†’ â„â‰¥0âˆ := fun a => â€–X aâ€–â‚‘
  let nY : Î© â†’ â„â‰¥0âˆ := fun a => â€–Y aâ€–â‚‘
  have hXY' : IndepFun nX nY Î¼ := hXY.comp measurable_enorm measurable_enorm
  have hnX : AEMeasurable nX Î¼ := hX.1.aemeasurable.enorm
  have hnY : AEMeasurable nY Î¼ := hY.1.aemeasurable.enorm
  have hmul : âˆ«â» a, nX a * nY a âˆ‚Î¼ = (âˆ«â» a, nX a âˆ‚Î¼) * âˆ«â» a, nY a âˆ‚Î¼ :=
    lintegral_mul_eq_lintegral_mul_lintegral_of_indepFun' hnX hnY hXY'
  refine âŸ¨hX.1.mul hY.1, ?_âŸ©
  simp only [nX, nY] at hmul
  simp_rw [hasFiniteIntegral_iff_enorm, Pi.mul_apply, enorm_mul, hmul]
  exact ENNReal.mul_lt_top hX.2 hY.2

/-- If the product of two independent real-valued random variables is integrable and
the second one is not almost everywhere zero, then the first one is integrable. -/
theorem IndepFun.integrable_left_of_integrable_mul {Î² : Type*} [MeasurableSpace Î²] {X Y : Î© â†’ Î²}
    [NormedDivisionRing Î²] [OpensMeasurableSpace Î²]
    (hXY : IndepFun X Y Î¼) (h'XY : Integrable (X * Y) Î¼)
    (hX : AEStronglyMeasurable X Î¼) (hY : AEStronglyMeasurable Y Î¼) (h'Y : Â¬Y =áµ[Î¼] 0) :
    Integrable X Î¼ := by
  refine âŸ¨hX, ?_âŸ©
  have I : (âˆ«â» Ï‰, â€–Y Ï‰â€–â‚‘ âˆ‚Î¼) â‰  0 := fun H â†¦ by
    have I : (fun Ï‰ => â€–Y Ï‰â€–â‚‘ : Î© â†’ â„â‰¥0âˆ) =áµ[Î¼] 0 := (lintegral_eq_zero_iff' hY.enorm).1 H
    apply h'Y
    filter_upwards [I] with Ï‰ hÏ‰
    simpa using hÏ‰
  refine hasFiniteIntegral_iff_enorm.mpr <| lt_top_iff_ne_top.2 fun H => ?_
  have J : IndepFun (â€–X Â·â€–â‚‘) (â€–Y Â·â€–â‚‘) Î¼ := hXY.comp measurable_enorm measurable_enorm
  have A : âˆ«â» Ï‰, â€–X Ï‰ * Y Ï‰â€–â‚‘ âˆ‚Î¼ < âˆ := h'XY.2
  simp only [enorm_mul] at A
  rw [lintegral_mul_eq_lintegral_mul_lintegral_of_indepFun'' hX.enorm hY.enorm J, H] at A
  simp only [ENNReal.top_mul I, lt_self_iff_false] at A

/-- If the product of two independent real-valued random variables is integrable and the
first one is not almost everywhere zero, then the second one is integrable. -/
theorem IndepFun.integrable_right_of_integrable_mul {Î² : Type*} [MeasurableSpace Î²] {X Y : Î© â†’ Î²}
    [NormedDivisionRing Î²] [OpensMeasurableSpace Î²]
    (hXY : IndepFun X Y Î¼) (h'XY : Integrable (X * Y) Î¼)
    (hX : AEStronglyMeasurable X Î¼) (hY : AEStronglyMeasurable Y Î¼) (h'X : Â¬X =áµ[Î¼] 0) :
    Integrable Y Î¼ := by
  refine âŸ¨hY, ?_âŸ©
  have I : âˆ«â» Ï‰, â€–X Ï‰â€–â‚‘ âˆ‚Î¼ â‰  0 := fun H â†¦ by
    have I : ((â€–X Â·â€–â‚‘) : Î© â†’ â„â‰¥0âˆ) =áµ[Î¼] 0 := (lintegral_eq_zero_iff' hX.enorm).1 H
    apply h'X
    filter_upwards [I] with Ï‰ hÏ‰
    simpa using hÏ‰
  refine lt_top_iff_ne_top.2 fun H => ?_
  have J : IndepFun (fun Ï‰ => â€–X Ï‰â€–â‚‘ : Î© â†’ â„â‰¥0âˆ) (fun Ï‰ => â€–Y Ï‰â€–â‚‘ : Î© â†’ â„â‰¥0âˆ) Î¼ :=
    IndepFun.comp hXY measurable_enorm measurable_enorm
  have A : âˆ«â» Ï‰, â€–X Ï‰ * Y Ï‰â€–â‚‘ âˆ‚Î¼ < âˆ := h'XY.2
  simp only [enorm_mul] at A
  rw [lintegral_mul_eq_lintegral_mul_lintegral_of_indepFun'' hX.enorm hY.enorm J, H] at A
  simp only [ENNReal.mul_top I, lt_self_iff_false] at A

lemma IndepFun.integral_fun_comp_mul_comp {ğ“§ ğ“¨ : Type*} {mğ“§ : MeasurableSpace ğ“§}
    {mğ“¨ : MeasurableSpace ğ“¨} {X : Î© â†’ ğ“§} {Y : Î© â†’ ğ“¨} {f : ğ“§ â†’ ğ•œ} {g : ğ“¨ â†’ ğ•œ}
    (hXY : IndepFun X Y Î¼) (hX : AEMeasurable X Î¼) (hY : AEMeasurable Y Î¼)
    (hf : AEStronglyMeasurable f (Î¼.map X)) (hg : AEStronglyMeasurable g (Î¼.map Y)) :
    âˆ« Ï‰, f (X Ï‰) * g (Y Ï‰) âˆ‚Î¼ = (âˆ« Ï‰, f (X Ï‰) âˆ‚Î¼) * âˆ« Ï‰, g (Y Ï‰) âˆ‚Î¼ := by
  have hfXgY := (hXY.compâ‚€ hX hY hf.aemeasurable hg.aemeasurable)
  have hfX := (hf.comp_aemeasurable hX)
  have hgY := (hg.comp_aemeasurable hY)
  by_cases h'X : âˆ€áµ Ï‰ âˆ‚Î¼, f (X Ï‰) = 0
  Â· have h' : âˆ€áµ Ï‰ âˆ‚Î¼, f (X Ï‰) * g (Y Ï‰) = 0 := by
      filter_upwards [h'X] with Ï‰ hÏ‰
      simp [hÏ‰]
    simp [integral_congr_ae h'X, integral_congr_ae h']
  by_cases h'Y : âˆ€áµ Ï‰ âˆ‚Î¼, g (Y Ï‰) = 0
  Â· have h' : âˆ€áµ Ï‰ âˆ‚Î¼, f (X Ï‰) * g (Y Ï‰) = 0 := by
      filter_upwards [h'Y] with Ï‰ hÏ‰
      simp [hÏ‰]
    simp [integral_congr_ae h'Y, integral_congr_ae h']
  by_cases h : Integrable (fun Ï‰ â†¦ f (X Ï‰) * g (Y Ï‰)) Î¼
  Â· have :=
      (hfXgY.integrable_left_of_integrable_mul h hfX hgY h'Y).isProbabilityMeasure_of_indepFun
        _ _ h'X hfXgY
    change âˆ« Ï‰, (fun x â†¦ f x.1 * g x.2) (X Ï‰, Y Ï‰) âˆ‚Î¼ = _
    rw [â† integral_map (f := fun x â†¦ f x.1 * g x.2) (Ï† := fun Ï‰ â†¦ (X Ï‰, Y Ï‰)),
      (indepFun_iff_map_prod_eq_prod_map_map hX hY).1 hXY, integral_prod_mul, integral_map,
      integral_map]
    any_goals fun_prop
    rw [(indepFun_iff_map_prod_eq_prod_map_map hX hY).1 hXY]
    exact hf.comp_fst.mul hg.comp_snd
  Â· rw [integral_undef h]
    obtain h | h : Â¬(Integrable (fun Ï‰ â†¦ f (X Ï‰)) Î¼) âˆ¨ Â¬(Integrable (fun Ï‰ â†¦ g (Y Ï‰)) Î¼) :=
      not_and_or.1 fun âŸ¨HX, HYâŸ© â†¦ h (hfXgY.integrable_mul HX HY)
    all_goals simp [integral_undef h]

lemma IndepFun.integral_comp_mul_comp {ğ“§ ğ“¨ : Type*} {mğ“§ : MeasurableSpace ğ“§}
    {mğ“¨ : MeasurableSpace ğ“¨} {X : Î© â†’ ğ“§} {Y : Î© â†’ ğ“¨} {f : ğ“§ â†’ ğ•œ} {g : ğ“¨ â†’ ğ•œ}
    (hXY : IndepFun X Y Î¼) (hX : AEMeasurable X Î¼) (hY : AEMeasurable Y Î¼)
    (hf : AEStronglyMeasurable f (Î¼.map X)) (hg : AEStronglyMeasurable g (Î¼.map Y)) :
    Î¼[(f âˆ˜ X) * (g âˆ˜ Y)] = Î¼[f âˆ˜ X] * Î¼[g âˆ˜ Y] :=
  hXY.integral_fun_comp_mul_comp hX hY hf hg

lemma IndepFun.integral_mul_eq_mul_integral
    (hXY : IndepFun X Y Î¼) (hX : AEStronglyMeasurable X Î¼) (hY : AEStronglyMeasurable Y Î¼) :
    Î¼[X * Y] = Î¼[X] * Î¼[Y] :=
  hXY.integral_comp_mul_comp hX.aemeasurable hY.aemeasurable
    aestronglyMeasurable_id aestronglyMeasurable_id

lemma IndepFun.integral_fun_mul_eq_mul_integral
    (hXY : IndepFun X Y Î¼) (hX : AEStronglyMeasurable X Î¼) (hY : AEStronglyMeasurable Y Î¼) :
    âˆ« Ï‰, X Ï‰ * Y Ï‰ âˆ‚Î¼ = Î¼[X] * Î¼[Y] :=
  hXY.integral_mul_eq_mul_integral hX hY

@[deprecated (since := "2025-07-30")] alias IndepFun.integral_mul_of_nonneg :=
  IndepFun.integral_mul_eq_mul_integral

@[deprecated (since := "2025-07-30")] alias IndepFun.integral_mul_of_integrable :=
  IndepFun.integral_mul_eq_mul_integral

@[deprecated (since := "2025-07-30")] alias IndepFun.integral_mul :=
  IndepFun.integral_mul_eq_mul_integral

@[deprecated (since := "2025-07-30")] alias IndepFun.integral_mul' :=
  IndepFun.integral_fun_mul_eq_mul_integral

/-- Independence of functions `f` and `g` into arbitrary types is characterized by the relation
  `E[(Ï† âˆ˜ f) * (Ïˆ âˆ˜ g)] = E[Ï† âˆ˜ f] * E[Ïˆ âˆ˜ g]` for all measurable `Ï†` and `Ïˆ` with values in `â„`
  satisfying appropriate integrability conditions. -/
theorem indepFun_iff_integral_comp_mul [IsFiniteMeasure Î¼] {Î² Î²' : Type*} {mÎ² : MeasurableSpace Î²}
    {mÎ²' : MeasurableSpace Î²'} {f : Î© â†’ Î²} {g : Î© â†’ Î²'} {hfm : Measurable f} {hgm : Measurable g} :
    IndepFun f g Î¼ â†” âˆ€ {Ï† : Î² â†’ â„} {Ïˆ : Î²' â†’ â„}, Measurable Ï† â†’ Measurable Ïˆ â†’
      Integrable (Ï† âˆ˜ f) Î¼ â†’ Integrable (Ïˆ âˆ˜ g) Î¼ â†’
        integral Î¼ (Ï† âˆ˜ f * Ïˆ âˆ˜ g) = integral Î¼ (Ï† âˆ˜ f) * integral Î¼ (Ïˆ âˆ˜ g) := by
  refine âŸ¨fun hfg _ _ hÏ† hÏˆ _ _ => hfg.integral_comp_mul_comp
      hfm.aemeasurable hgm.aemeasurable hÏ†.aestronglyMeasurable hÏˆ.aestronglyMeasurable, ?_âŸ©
  rw [IndepFun_iff]
  rintro h _ _ âŸ¨A, hA, rflâŸ© âŸ¨B, hB, rflâŸ©
  specialize
    h (measurable_one.indicator hA) (measurable_one.indicator hB)
      ((integrable_const 1).indicator (hfm.comp measurable_id hA))
      ((integrable_const 1).indicator (hgm.comp measurable_id hB))
  rwa [â† ENNReal.toReal_eq_toReal (measure_ne_top Î¼ _), ENNReal.toReal_mul, â† measureReal_def,
    â† measureReal_def, â† measureReal_def, â† integral_indicator_one ((hfm hA).inter (hgm hB)),
    â† integral_indicator_one (hfm hA), â† integral_indicator_one (hgm hB), Set.inter_indicator_one]
  exact ENNReal.mul_ne_top (measure_ne_top Î¼ _) (measure_ne_top Î¼ _)

variable {Î¹ : Type*} [Fintype Î¹] {ğ“§ : Î¹ â†’ Type*} {mğ“§ : âˆ€ i, MeasurableSpace (ğ“§ i)}
    {X : (i : Î¹) â†’ Î© â†’ ğ“§ i} {f : (i : Î¹) â†’ ğ“§ i â†’ ğ•œ}

lemma iIndepFun.integral_fun_prod_comp (hX : iIndepFun X Î¼)
    (mX : âˆ€ i, AEMeasurable (X i) Î¼) (hf : âˆ€ i, AEStronglyMeasurable (f i) (Î¼.map (X i))) :
    âˆ« Ï‰, âˆ i, f i (X i Ï‰) âˆ‚Î¼ = âˆ i, âˆ« Ï‰, f i (X i Ï‰) âˆ‚Î¼ := by
  have := hX.isProbabilityMeasure
  change âˆ« Ï‰, (fun x â†¦ âˆ i, f i (x i)) (X Â· Ï‰) âˆ‚Î¼ = _
  rw [â† integral_map (f := fun x â†¦ âˆ i, f i (x i)) (Ï† := fun Ï‰ â†¦ (X Â· Ï‰)),
    (iIndepFun_iff_map_fun_eq_pi_map mX).1 hX, integral_fintype_prod_eq_prod]
  Â· congr with i
    rw [integral_map (mX i) (hf i)]
  Â· fun_prop
  rw [(iIndepFun_iff_map_fun_eq_pi_map mX).1 hX]
  exact Finset.aestronglyMeasurable_fun_prod Finset.univ fun i _ â†¦
    (hf i).comp_quasiMeasurePreserving (Measure.quasiMeasurePreserving_eval _ i)

lemma iIndepFun.integral_prod_comp (hX : iIndepFun X Î¼)
    (mX : âˆ€ i, AEMeasurable (X i) Î¼) (hf : âˆ€ i, AEStronglyMeasurable (f i) (Î¼.map (X i))) :
    Î¼[âˆ i, (f i) âˆ˜ (X i)] = âˆ i, Î¼[(f i) âˆ˜ (X i)] := by
  convert hX.integral_fun_prod_comp mX hf
  simp

variable {X : (i : Î¹) â†’ Î© â†’ ğ•œ}

lemma iIndepFun.integral_prod_eq_prod_integral
    (hX : iIndepFun X Î¼) (mX : âˆ€ i, AEStronglyMeasurable (X i) Î¼) :
    Î¼[âˆ i, X i] = âˆ i, Î¼[X i] :=
  hX.integral_prod_comp (fun i â†¦ (mX i).aemeasurable) (fun _ â†¦ aestronglyMeasurable_id)

lemma iIndepFun.integral_fun_prod_eq_prod_integral
    (hX : iIndepFun X Î¼) (mX : âˆ€ i, AEStronglyMeasurable (X i) Î¼) :
    âˆ« Ï‰, âˆ i, X i Ï‰ âˆ‚Î¼ = âˆ i, Î¼[X i] :=
  hX.integral_fun_prod_comp (fun i â†¦ (mX i).aemeasurable) (fun _ â†¦ aestronglyMeasurable_id)

end ProbabilityTheory
