/-
Copyright (c) 2025 Etienne Marion. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Etienne Marion
-/
import Mathlib.Probability.Independence.Basic
import Mathlib.Probability.ProductMeasure

/-!
# Random variables are independent iff their joint distribution is the product measure.

There are several possible measurability assumptions:
* The map `Ï‰ â†¦ (Xáµ¢(Ï‰))áµ¢` is measurable.
* For all `i`, the map `Ï‰ â†¦ Xáµ¢(Ï‰)` is measurable.
* The map `Ï‰ â†¦ (Xáµ¢(Ï‰))áµ¢` is almost everywhere measurable.
* For all `i`, the map `Ï‰ â†¦ Xáµ¢(Ï‰)` is almost everywhere measurable.
Although the first two options are equivalent, the last two are not if the index set is not
countable. Therefore we first prove the third case `iIndepFun_iff_map_fun_eq_infinitePi_mapâ‚€`,
then deduce the fourth case in `iIndepFun_iff_map_fun_eq_infinitePi_mapâ‚€'` (assuming the index
type is countable), and we prove the first case in `iIndepFun_iff_map_fun_eq_infinitePi_map`.
-/

open MeasureTheory Measure ProbabilityTheory

namespace ProbabilityTheory

variable {Î¹ Î© : Type*} {mÎ© : MeasurableSpace Î©} {P : Measure Î©} [IsProbabilityMeasure P]
    {ğ“§ : Î¹ â†’ Type*} {mğ“§ : âˆ€ i, MeasurableSpace (ğ“§ i)} {X : Î  i, Î© â†’ ğ“§ i}

/-- Random variables are independent iff their joint distribution is the product measure. This
is a version where the random variable `Ï‰ â†¦ (Xáµ¢(Ï‰))áµ¢` is almost everywhere measurable.
See `iIndepFun_iff_map_fun_eq_infinitePi_mapâ‚€'` for a version which only assumes that
each `Xáµ¢` is almost everywhere measurable and that `Î¹` is countable. -/
lemma iIndepFun_iff_map_fun_eq_infinitePi_mapâ‚€ (mX : AEMeasurable (fun Ï‰ i â†¦ X i Ï‰) P) :
    iIndepFun X P â†” P.map (fun Ï‰ i â†¦ X i Ï‰) = infinitePi (fun i â†¦ P.map (X i)) where
  mp h := by
    have _ i := isProbabilityMeasure_map (mX.eval i)
    refine eq_infinitePi _ fun s t ht â†¦ ?_
    rw [iIndepFun_iff_finset] at h
    have : s.toSet.pi t = s.restrict â»Â¹' (Set.univ.pi fun i â†¦ t i) := by ext; simp
    rw [this, â† map_apply, AEMeasurable.map_map_of_aemeasurable]
    Â· have : s.restrict âˆ˜ (fun Ï‰ i â†¦ X i Ï‰) = fun Ï‰ i â†¦ s.restrict X i Ï‰ := by ext; simp
      rw [this, (iIndepFun_iff_map_fun_eq_pi_map ?_).1 (h s), pi_pi]
      Â· simp only [Finset.restrict]
        rw [s.prod_coe_sort fun i â†¦ P.map (X i) (t i)]
      exact fun i â†¦ mX.eval i
    any_goals fun_prop
    Â· exact mX
    Â· exact .univ_pi fun i â†¦ ht i i.2
  mpr h := by
    have _ i := isProbabilityMeasure_map (mX.eval i)
    rw [iIndepFun_iff_finset]
    intro s
    rw [iIndepFun_iff_map_fun_eq_pi_map]
    Â· have : s.restrict âˆ˜ (fun Ï‰ i â†¦ X i Ï‰) = fun Ï‰ i â†¦ s.restrict X i Ï‰ := by ext; simp
      rw [â† this, â† AEMeasurable.map_map_of_aemeasurable, h, infinitePi_map_restrict]
      Â· simp
      Â· fun_prop
      exact mX
    exact fun i â†¦ mX.eval i

/-- Random variables are independent iff their joint distribution is the product measure. This is
an `AEMeasurable` version of `iIndepFun_iff_map_fun_eq_infinitePi_map`, which is why it requires
`Î¹` to be countable. -/
lemma iIndepFun_iff_map_fun_eq_infinitePi_mapâ‚€' [Countable Î¹] (mX : âˆ€ i, AEMeasurable (X i) P) :
    iIndepFun X P â†” P.map (fun Ï‰ i â†¦ X i Ï‰) = infinitePi (fun i â†¦ P.map (X i)) :=
  iIndepFun_iff_map_fun_eq_infinitePi_mapâ‚€ <| aemeasurable_pi_iff.2 mX

/-- Random variables are independent iff their joint distribution is the product measure. -/
lemma iIndepFun_iff_map_fun_eq_infinitePi_map (mX : âˆ€ i, Measurable (X i)) :
    iIndepFun X P â†” P.map (fun Ï‰ i â†¦ X i Ï‰) = infinitePi (fun i â†¦ P.map (X i)) :=
  iIndepFun_iff_map_fun_eq_infinitePi_mapâ‚€ <| measurable_pi_iff.2 mX |>.aemeasurable

/-- Given random variables `X i : Î© i â†’ ğ“§ i`, they are independent when viewed as random
variables defined on the product space `Î  i, Î© i`. -/
lemma iIndepFun_infinitePi {Î© : Î¹ â†’ Type*} {mÎ© : âˆ€ i, MeasurableSpace (Î© i)}
    {P : (i : Î¹) â†’ Measure (Î© i)} [âˆ€ i, IsProbabilityMeasure (P i)] {X : (i : Î¹) â†’ Î© i â†’ ğ“§ i}
    (mX : âˆ€ i, Measurable (X i)) :
    iIndepFun (fun i Ï‰ â†¦ X i (Ï‰ i)) (infinitePi P) := by
  refine iIndepFun_iff_map_fun_eq_infinitePi_map (by fun_prop) |>.2 ?_
  rw [infinitePi_map_pi _ mX]
  congr
  ext i : 1
  rw [â† (measurePreserving_eval_infinitePi P i).map_eq, map_map (mX i) (by fun_prop),
    Function.comp_def]

section curry

open Sigma

omit [IsProbabilityMeasure P]

/-- Consider `((Xáµ¢â±¼)â±¼)áµ¢` a family of families of random variables.
Assume that for any `i`, the random variables `(Xáµ¢â±¼)â±¼` are independent.
Assume furthermore that the random variables `((Xáµ¢â±¼)â±¼)áµ¢` are independent.
Then the random variables `(Xáµ¢â±¼)` indexed by pairs `(i, j)` are independent.

This is a dependent version of `iIndepFun_uncurry'`. -/
lemma iIndepFun_uncurry {Îº : Î¹ â†’ Type*} {ğ“§ : (i : Î¹) â†’ Îº i â†’ Type*}
    {mğ“§ : âˆ€ i j, MeasurableSpace (ğ“§ i j)} {X : (i : Î¹) â†’ (j : Îº i) â†’ Î© â†’ ğ“§ i j}
    (mX : âˆ€ i j, Measurable (X i j)) (h1 : iIndepFun (fun i Ï‰ â†¦ (X i Â· Ï‰)) P)
    (h2 : âˆ€ i, iIndepFun (X i) P) :
    iIndepFun (fun (p : (i : Î¹) Ã— (Îº i)) Ï‰ â†¦ X p.1 p.2 Ï‰) P := by
  have := h1.isProbabilityMeasure
  have : âˆ€ i j, IsProbabilityMeasure (P.map (X i j)) :=
    fun i j â†¦ isProbabilityMeasure_map (mX i j).aemeasurable
  have : âˆ€ i, IsProbabilityMeasure (P.map (fun Ï‰ â†¦ (X i Â· Ï‰))) :=
    fun i â†¦ isProbabilityMeasure_map (Measurable.aemeasurable (by fun_prop))
  have : (MeasurableEquiv.piCurry ğ“§) âˆ˜ (fun Ï‰ p â†¦ X p.1 p.2 Ï‰) = fun Ï‰ i j â†¦ X i j Ï‰ := by
    ext; simp [curry]
  rw [iIndepFun_iff_map_fun_eq_infinitePi_map (by fun_prop),
    â† (MeasurableEquiv.piCurry ğ“§).map_measurableEquiv_injective.eq_iff,
    map_map (by fun_prop) (by fun_prop), this,
    (iIndepFun_iff_map_fun_eq_infinitePi_map (by fun_prop)).1 h1,
    infinitePi_map_piCurry (fun i j â†¦ P.map (X i j))]
  congrm infinitePi (fun i â†¦ ?_)
  rw [(iIndepFun_iff_map_fun_eq_infinitePi_map (by fun_prop)).1 (h2 i)]

/-- Consider `((Xáµ¢â±¼)â±¼)áµ¢` a family of families of random variables.
Assume that for any `i`, the random variables `(Xáµ¢â±¼)â±¼` are independent.
Assume furthermore that the random variables `((Xáµ¢â±¼)â±¼)áµ¢` are independent.
Then the random variables `(Xáµ¢â±¼)` indexed by pairs `(i, j)` are independent.

This is a non-dependent version of `iIndepFun_uncurry`. -/
lemma iIndepFun_uncurry' {Îº : Type*} {ğ“§ : Î¹ â†’ Îº â†’ Type*} {mğ“§ : âˆ€ i j, MeasurableSpace (ğ“§ i j)}
    {X : (i : Î¹) â†’ (j : Îº) â†’ Î© â†’ ğ“§ i j} (mX : âˆ€ i j, Measurable (X i j))
    (h1 : iIndepFun (fun i Ï‰ â†¦ (X i Â· Ï‰)) P) (h2 : âˆ€ i, iIndepFun (X i) P) :
    iIndepFun (fun (p : Î¹ Ã— Îº) Ï‰ â†¦ X p.1 p.2 Ï‰) P :=
  (iIndepFun_uncurry mX h1 h2).of_precomp (Equiv.sigmaEquivProd Î¹ Îº).surjective

end curry

end ProbabilityTheory
