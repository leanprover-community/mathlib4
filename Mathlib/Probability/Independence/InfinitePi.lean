/-
Copyright (c) 2025 Etienne Marion. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Etienne Marion
-/
import Mathlib.Probability.Independence.Basic
import Mathlib.Probability.ProductMeasure

/-!
# Random variables are independent iff their joint distribution is the product measure.
-/

open MeasureTheory Measure ProbabilityTheory

namespace ProbabilityTheory

variable {Î¹ Î© : Type*} {mÎ© : MeasurableSpace Î©} {Î¼ : Measure Î©} [IsProbabilityMeasure Î¼]
    {ğ’³ : Î¹ â†’ Type*} {mğ’³ : âˆ€ i, MeasurableSpace (ğ’³ i)} (X : Î  i, Î© â†’ ğ’³ i)

/-- Random variables are independent iff their joint distribution is the product measure. -/
lemma iIndepFun_iff_map_fun_eq_infinitePi_map (mX : âˆ€ i, Measurable (X i)) :
    haveI := fun i â†¦ isProbabilityMeasure_map (Î¼ := Î¼) (mX i).aemeasurable
    iIndepFun X Î¼ â†” Î¼.map (fun Ï‰ i â†¦ X i Ï‰) = (infinitePi (fun i â†¦ Î¼.map (X i))) where
  mp h := by
    haveI := fun i â†¦ isProbabilityMeasure_map (Î¼ := Î¼) (mX i).aemeasurable
    apply eq_infinitePi
    intro s t ht
    rw [iIndepFun_iff_finite] at h
    have : s.toSet.pi t = s.restrict â»Â¹' ((@Set.univ s ).pi fun i â†¦ t i) := by ext; simp
    rw [this, â† map_apply, map_map]
    Â· have : s.restrict âˆ˜ (fun Ï‰ i â†¦ X i Ï‰) = fun Ï‰ i â†¦ s.restrict X i Ï‰ := by ext; simp
      rw [this, (iIndepFun_iff_map_fun_eq_pi_map ?_).1 (h s), pi_pi]
      Â· simp only [Finset.restrict]
        rw [s.prod_coe_sort fun i â†¦ Î¼.map (X i) (t i)]
      exact fun i â†¦ (mX i).aemeasurable
    any_goals fun_prop
    exact MeasurableSet.univ_pi fun i â†¦ ht i i.2
  mpr h := by
    rw [iIndepFun_iff_finite]
    intro s
    rw [iIndepFun_iff_map_fun_eq_pi_map]
    Â· have : s.restrict âˆ˜ (fun Ï‰ i â†¦ X i Ï‰) = fun Ï‰ i â†¦ s.restrict X i Ï‰ := by ext; simp
      rw [â† this, â† map_map, h, infinitePi_map_restrict]
      Â· simp
      all_goals fun_prop
    exact fun i â†¦ (mX i).aemeasurable

/-- Random variables are independent iff their joint distribution is the product measure. This is
an `AEMeasurable` version of `iIndepFun_iff_map_fun_eq_infinitePi_map`, which is why it requires
`Î¹` to be countable. -/
lemma iIndepFun_iff_map_fun_eq_infinitePi_mapâ‚€ [Countable Î¹] (mX : âˆ€ i, AEMeasurable (X i) Î¼) :
    haveI _ i := isProbabilityMeasure_map (mX i)
    iIndepFun X Î¼ â†” Î¼.map (fun Ï‰ i â†¦ X i Ï‰) = (infinitePi (fun i â†¦ Î¼.map (X i))) := by
  rw [iIndepFun_congr_iff (fun i â†¦ (mX i).ae_eq_mk), iIndepFun_iff_map_fun_eq_infinitePi_map]
  Â· have : (fun Ï‰ i â†¦ (mX i).mk (X i) Ï‰) =áµ[Î¼] fun Ï‰ i â†¦ X i Ï‰ := by
      change âˆ€áµ Ï‰ âˆ‚Î¼, (fun i â†¦ (mX i).mk (X i) Ï‰) = fun i â†¦ X i Ï‰
      simp_rw [funext_iff]
      rw [MeasureTheory.ae_all_iff]
      exact fun i â†¦ (mX i).ae_eq_mk.symm
    simp_rw [Measure.map_congr this, fun i â†¦ Measure.map_congr (mX i).ae_eq_mk.symm]
  exact fun i â†¦ (mX i).measurable_mk

end ProbabilityTheory
