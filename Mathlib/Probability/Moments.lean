/-
Copyright (c) 2022 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import Mathlib.Probability.Variance
import Mathlib.Analysis.SpecialFunctions.ContinuousFunctionalCalculus.ExpLog
import Mathlib.Analysis.Calculus.SmoothSeries
import Mathlib.Analysis.Analytic.OfScalars

/-!
# Moments and moment generating function

## Main definitions

* `ProbabilityTheory.moment X p Œº`: `p`th moment of a real random variable `X` with respect to
  measure `Œº`, `Œº[X^p]`
* `ProbabilityTheory.centralMoment X p Œº`:`p`th central moment of `X` with respect to measure `Œº`,
  `Œº[(X - Œº[X])^p]`
* `ProbabilityTheory.mgf X Œº t`: moment generating function of `X` with respect to measure `Œº`,
  `Œº[exp(t*X)]`
* `ProbabilityTheory.cgf X Œº t`: cumulant generating function, logarithm of the moment generating
  function

## Main results

* `ProbabilityTheory.IndepFun.mgf_add`: if two real random variables `X` and `Y` are independent
  and their mgfs are defined at `t`, then `mgf (X + Y) Œº t = mgf X Œº t * mgf Y Œº t`
* `ProbabilityTheory.IndepFun.cgf_add`: if two real random variables `X` and `Y` are independent
  and their cgfs are defined at `t`, then `cgf (X + Y) Œº t = cgf X Œº t + cgf Y Œº t`
* `ProbabilityTheory.measure_ge_le_exp_cgf` and `ProbabilityTheory.measure_le_le_exp_cgf`:
  Chernoff bound on the upper (resp. lower) tail of a random variable. For `t` nonnegative such that
  the cgf exists, `‚Ñô(Œµ ‚â§ X) ‚â§ exp(- t*Œµ + cgf X ‚Ñô t)`. See also
  `ProbabilityTheory.measure_ge_le_exp_mul_mgf` and
  `ProbabilityTheory.measure_le_le_exp_mul_mgf` for versions of these results using `mgf` instead
  of `cgf`.

-/


open MeasureTheory Filter Finset Real

noncomputable section

open scoped MeasureTheory ProbabilityTheory ENNReal NNReal Topology

-- found on zulip
theorem Real.exp_eq_tsum (x : ‚Ñù) :
    Real.exp x = ‚àë' n, x^n / n.factorial := by
  rw [Real.exp_eq_exp_‚Ñù, NormedSpace.exp_eq_tsum_div]

lemma _root_.Summable.mono {Œ≤ E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
    [FiniteDimensional ‚Ñù E] {f g : Œ≤ ‚Üí E} (hg : Summable g)
    (hfg : ‚àÄ b, ‚Äñf b‚Äñ ‚â§ ‚Äñg b‚Äñ) :
    Summable f := by
  rw [‚Üê summable_norm_iff] at hg ‚ä¢
  refine summable_of_sum_le (c := ‚àë' x, ‚Äñg x‚Äñ) (fun _ ‚Ü¶ by positivity) (fun s ‚Ü¶ ?_)
  exact (sum_le_sum fun i _ ‚Ü¶ hfg i).trans (sum_le_tsum s (fun _ _ ‚Ü¶ by positivity) hg)

namespace ProbabilityTheory

variable {Œ© Œπ : Type*} {m : MeasurableSpace Œ©} {X : Œ© ‚Üí ‚Ñù} {p : ‚Ñï} {Œº : Measure Œ©}

/-- Moment of a real random variable, `Œº[X ^ p]`. -/
def moment (X : Œ© ‚Üí ‚Ñù) (p : ‚Ñï) (Œº : Measure Œ©) : ‚Ñù :=
  Œº[X ^ p]

/-- Central moment of a real random variable, `Œº[(X - Œº[X]) ^ p]`. -/
def centralMoment (X : Œ© ‚Üí ‚Ñù) (p : ‚Ñï) (Œº : Measure Œ©) : ‚Ñù := by
  have m := fun (x : Œ©) => Œº[X] -- Porting note: Lean deems `Œº[(X - fun x => Œº[X]) ^ p]` ambiguous
  exact Œº[(X - m) ^ p]

@[simp]
theorem moment_zero (hp : p ‚â† 0) : moment 0 p Œº = 0 := by
  simp only [moment, hp, zero_pow, Ne, not_false_iff, Pi.zero_apply, integral_const,
    smul_eq_mul, mul_zero, integral_zero]

@[simp]
theorem centralMoment_zero (hp : p ‚â† 0) : centralMoment 0 p Œº = 0 := by
  simp only [centralMoment, hp, Pi.zero_apply, integral_const, smul_eq_mul,
    mul_zero, zero_sub, Pi.pow_apply, Pi.neg_apply, neg_zero, zero_pow, Ne, not_false_iff]

theorem centralMoment_one' [IsFiniteMeasure Œº] (h_int : Integrable X Œº) :
    centralMoment X 1 Œº = (1 - (Œº Set.univ).toReal) * Œº[X] := by
  simp only [centralMoment, Pi.sub_apply, pow_one]
  rw [integral_sub h_int (integrable_const _)]
  simp only [sub_mul, integral_const, smul_eq_mul, one_mul]

@[simp]
theorem centralMoment_one [IsZeroOrProbabilityMeasure Œº] : centralMoment X 1 Œº = 0 := by
  rcases eq_zero_or_isProbabilityMeasure Œº with rfl | h
  ¬∑ simp [centralMoment]
  by_cases h_int : Integrable X Œº
  ¬∑ rw [centralMoment_one' h_int]
    simp only [measure_univ, ENNReal.one_toReal, sub_self, zero_mul]
  ¬∑ simp only [centralMoment, Pi.sub_apply, pow_one]
    have : ¬¨Integrable (fun x => X x - integral Œº X) Œº := by
      refine fun h_sub => h_int ?_
      have h_add : X = (fun x => X x - integral Œº X) + fun _ => integral Œº X := by ext1 x; simp
      rw [h_add]
      exact h_sub.add (integrable_const _)
    rw [integral_undef this]

theorem centralMoment_two_eq_variance [IsFiniteMeasure Œº] (hX : Mem‚Ñíp X 2 Œº) :
    centralMoment X 2 Œº = variance X Œº := by rw [hX.variance_eq]; rfl

section MomentGeneratingFunction

variable {t u v : ‚Ñù}

lemma _root_.AEMeasurable.abs (hX : AEMeasurable X Œº) : AEMeasurable (fun œâ ‚Ü¶ |X œâ|) Œº :=
  hX.max (aemeasurable_neg_iff.mpr hX)

lemma aemeasurable_of_aemeasurable_exp_mul (ht : t ‚â† 0)
    (hX : AEMeasurable (fun œâ ‚Ü¶ exp (t * X œâ)) Œº) :
    AEMeasurable X Œº := by
  suffices AEMeasurable (fun œâ ‚Ü¶ t * X œâ) Œº by
    have h_eq : X = fun œâ ‚Ü¶ (t * X œâ) / t := by ext œâ; field_simp
    rw [h_eq]
    exact this.div aemeasurable_const
  exact aemeasurable_of_aemeasurable_exp hX

section Integrable

/-- Auxiliary lemma for `integrable_exp_mul_of_le`. -/
lemma integrable_exp_mul_of_le_of_measurable [IsFiniteMeasure Œº] (hX : Measurable X)
    (hu : Integrable (fun œâ ‚Ü¶ exp (u * X œâ)) Œº) (h_nonneg : 0 ‚â§ t) (htu : t ‚â§ u) :
    Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) Œº := by
  by_cases ht : t = 0
  ¬∑ simp [ht]
  have h_pos : 0 < t := lt_of_le_of_ne' h_nonneg ht
  have hu' : Integrable (1 + {w | 0 ‚â§ X w}.indicator (fun œâ ‚Ü¶ exp (u * X œâ))) Œº :=
    (integrable_const _).add (hu.indicator (hX measurableSet_Ici))
  refine hu'.mono ?_ (ae_of_all _ fun œâ ‚Ü¶ ?_)
  ¬∑ have hX : AEMeasurable X Œº := aemeasurable_of_aemeasurable_exp_mul (h_pos.trans_le htu).ne'
      hu.aemeasurable
    exact (measurable_exp.comp_aemeasurable (hX.const_mul _)).aestronglyMeasurable
  ¬∑ simp only [norm_eq_abs, abs_exp, Pi.add_apply, Pi.one_apply]
    rw [abs_of_nonneg]
    swap; ¬∑ exact add_nonneg zero_le_one (Set.indicator_nonneg (fun œâ _ ‚Ü¶ by positivity) _)
    rcases le_or_lt 0 (X œâ) with h_nonneg | h_neg
    ¬∑ simp only [Set.mem_setOf_eq, h_nonneg, Set.indicator_of_mem]
      calc rexp (t * X œâ) ‚â§ 1 + rexp (t * X œâ) := (le_add_iff_nonneg_left _).mpr zero_le_one
      _ ‚â§ 1 + exp (u * X œâ) := by gcongr
    ¬∑ simp only [Set.mem_setOf_eq, not_le.mpr h_neg, not_false_eq_true, Set.indicator_of_not_mem,
        add_zero, exp_le_one_iff]
      exact mul_nonpos_of_nonneg_of_nonpos h_pos.le h_neg.le

/-- If `œâ ‚Ü¶ exp (u * X œâ)` is integrable at `u ‚â• 0`, then it is integrable on `[0, u]`. -/
lemma integrable_exp_mul_of_le [IsFiniteMeasure Œº]
    (hu : Integrable (fun œâ ‚Ü¶ exp (u * X œâ)) Œº) (h_nonneg : 0 ‚â§ t) (htu : t ‚â§ u) :
    Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) Œº := by
  by_cases ht : t = 0
  ¬∑ simp [ht]
  have hX : AEMeasurable X Œº := by
    refine aemeasurable_of_aemeasurable_exp_mul ?_ hu.1.aemeasurable
    exact ((lt_of_le_of_ne' h_nonneg ht).trans_le htu).ne'
  have h_eq t : (fun œâ ‚Ü¶ exp (t * X œâ)) =·µê[Œº] fun œâ ‚Ü¶ exp (t * hX.mk X œâ) := by
    filter_upwards [hX.ae_eq_mk] with œâ hœâ using by rw [hœâ]
  rw [integrable_congr (h_eq t)]
  rw [integrable_congr (h_eq u)] at hu
  exact integrable_exp_mul_of_le_of_measurable hX.measurable_mk hu h_nonneg htu

/-- Auxiliary lemma for `integrable_exp_mul_of_ge`. -/
lemma integrable_exp_mul_of_ge_of_measurable [IsFiniteMeasure Œº] (hX : Measurable X)
    (hu : Integrable (fun œâ ‚Ü¶ exp (u * X œâ)) Œº) (h_nonpos : t ‚â§ 0) (htu : u ‚â§ t) :
    Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) Œº := by
  by_cases ht : t = 0
  ¬∑ simp [ht]
  have h_neg : t < 0 := lt_of_le_of_ne h_nonpos ht
  have hu' : Integrable (1 + {w | X w ‚â§ 0}.indicator (fun œâ ‚Ü¶ exp (u * X œâ))) Œº :=
    (integrable_const _).add (hu.indicator (hX measurableSet_Iic))
  refine hu'.mono ?_ (ae_of_all _ fun œâ ‚Ü¶ ?_)
  ¬∑ have hX : AEMeasurable X Œº := aemeasurable_of_aemeasurable_exp_mul (htu.trans_lt h_neg).ne
      hu.aemeasurable
    exact (measurable_exp.comp_aemeasurable (hX.const_mul _)).aestronglyMeasurable
  ¬∑ simp only [norm_eq_abs, abs_exp, Pi.add_apply, Pi.one_apply]
    rw [abs_of_nonneg]
    swap; ¬∑ exact add_nonneg zero_le_one (Set.indicator_nonneg (fun œâ _ ‚Ü¶ by positivity) _)
    rcases lt_or_le 0 (X œâ) with h_pos | h_nonpos
    ¬∑ simp only [Set.mem_setOf_eq, not_le, h_pos, Set.indicator_of_not_mem, add_zero,
        exp_le_one_iff]
      exact mul_nonpos_of_nonpos_of_nonneg h_neg.le h_pos.le
    ¬∑ simp only [Set.mem_setOf_eq, h_nonpos, Set.indicator_of_mem]
      calc rexp (t * X œâ) ‚â§ 1 + rexp (t * X œâ) := (le_add_iff_nonneg_left _).mpr zero_le_one
      _ ‚â§ 1 + exp (u * X œâ) := by
        refine add_le_add le_rfl (exp_monotone ?_)
        exact mul_le_mul_of_nonpos_of_nonpos htu le_rfl (htu.trans h_neg.le) h_nonpos

/-- If `œâ ‚Ü¶ exp (u * X œâ)` is integrable at `u ‚â§ 0`, then it is integrable on `[u, 0]`. -/
lemma integrable_exp_mul_of_ge [IsFiniteMeasure Œº]
    (hu : Integrable (fun œâ ‚Ü¶ exp (u * X œâ)) Œº) (h_nonpos : t ‚â§ 0) (htu : u ‚â§ t) :
    Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) Œº := by
  by_cases ht : t = 0
  ¬∑ simp [ht]
  have hX : AEMeasurable X Œº := by
    refine aemeasurable_of_aemeasurable_exp_mul ?_ hu.1.aemeasurable
    refine (htu.trans_lt ?_).ne
    exact lt_of_le_of_ne h_nonpos ht
  have h_eq t : (fun œâ ‚Ü¶ exp (t * X œâ)) =·µê[Œº] fun œâ ‚Ü¶ exp (t * hX.mk X œâ) := by
    filter_upwards [hX.ae_eq_mk] with œâ hœâ using by rw [hœâ]
  rw [integrable_congr (h_eq t)]
  rw [integrable_congr (h_eq u)] at hu
  exact integrable_exp_mul_of_ge_of_measurable hX.measurable_mk hu h_nonpos htu

/-- If `œâ ‚Ü¶ exp (u * X œâ)` is integrable at `u` and `-u`, then it is integrable on `[-u, u]`. -/
lemma integrable_exp_mul_of_abs_le [IsFiniteMeasure Œº]
    (hu_int_pos : Integrable (fun œâ ‚Ü¶ exp (u * X œâ)) Œº)
    (hu_int_neg : Integrable (fun œâ ‚Ü¶ exp (- u * X œâ)) Œº)
    (htu : |t| ‚â§ |u|) :
    Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) Œº := by
  rcases le_total 0 t with ht | ht
  ¬∑ rw [abs_of_nonneg ht] at htu
    refine integrable_exp_mul_of_le ?_ ht htu
    rcases le_total 0 u with hu | hu
    ¬∑ rwa [abs_of_nonneg hu]
    ¬∑ rwa [abs_of_nonpos hu]
  ¬∑ rw [abs_of_nonpos ht, neg_le] at htu
    refine integrable_exp_mul_of_ge ?_ ht htu
    rcases le_total 0 u with hu | hu
    ¬∑ rwa [abs_of_nonneg hu]
    ¬∑ rwa [abs_of_nonpos hu, neg_neg]

lemma integrable_exp_mul_of_le_of_le [IsFiniteMeasure Œº] {a b : ‚Ñù}
    (ha : Integrable (fun œâ ‚Ü¶ exp (a * X œâ)) Œº)
    (hb : Integrable (fun œâ ‚Ü¶ exp (b * X œâ)) Œº)
    (hat : a ‚â§ t) (htb : t ‚â§ b) :
    Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) Œº := by
  rcases le_total 0 t with ht | ht
  ¬∑ exact integrable_exp_mul_of_le hb ht htb
  ¬∑ exact integrable_exp_mul_of_ge ha ht hat

lemma exp_mul_abs_add_le_add : exp (t * |u| + v * u) ‚â§ rexp ((v + t) * u) + rexp ((v - t) * u) := by
  rcases le_total 0 u with h_nonneg | h_nonpos
  ¬∑ rw [abs_of_nonneg h_nonneg, ‚Üê add_mul, add_comm, le_add_iff_nonneg_right]
    positivity
  ¬∑ rw [abs_of_nonpos h_nonpos, mul_neg, mul_comm, ‚Üê mul_neg, mul_comm, ‚Üê add_mul, add_comm,
      ‚Üê sub_eq_add_neg, le_add_iff_nonneg_left]
    positivity

lemma exp_mul_abs_le_add : exp (t * |u|) ‚â§ rexp (t * u) + rexp (-(t * u)) := by
  have h := exp_mul_abs_add_le_add (t := t) (u := u) (v := 0)
  simpa using h

lemma integrable_exp_mul_abs_add (ht_int_pos : Integrable (fun œâ ‚Ü¶ rexp ((v + t) * X œâ)) Œº)
    (ht_int_neg : Integrable (fun œâ ‚Ü¶ rexp ((v - t) * X œâ)) Œº) :
    Integrable (fun œâ ‚Ü¶ rexp (t * |X œâ| + v * X œâ)) Œº := by
  have h_int_add : Integrable (fun a ‚Ü¶ rexp ((v + t) * X a) + rexp ((v - t) * X a)) Œº :=
    ht_int_pos.add <| by simpa using ht_int_neg
  refine Integrable.mono h_int_add ?_ (ae_of_all _ fun œâ ‚Ü¶ ?_)
  ¬∑ by_cases ht : t = 0
    ¬∑ simp only [ht, zero_mul, zero_add]
      simp only [ht, add_zero] at ht_int_pos
      exact ht_int_pos.1
    have hX : AEMeasurable X Œº := by
      by_cases hvt : v + t = 0
      ¬∑ have hvt' : v - t ‚â† 0 := by
          rw [sub_ne_zero]
          refine fun h_eq ‚Ü¶ ht ?_
          simpa [h_eq] using hvt
        exact aemeasurable_of_aemeasurable_exp_mul hvt' ht_int_neg.1.aemeasurable
      ¬∑ exact aemeasurable_of_aemeasurable_exp_mul hvt ht_int_pos.1.aemeasurable
    refine AEMeasurable.aestronglyMeasurable ?_
    exact measurable_exp.comp_aemeasurable ((hX.abs.const_mul _).add (hX.const_mul _))
  ¬∑ simp only [norm_eq_abs, abs_exp]
    conv_rhs => rw [abs_of_nonneg (by positivity)]
    exact exp_mul_abs_add_le_add

/-- If `œâ ‚Ü¶ rexp (t * X œâ)` is integrable at `t` and `-t`, then `œâ ‚Ü¶ rexp (t * |X œâ|)` is
integrable. -/
lemma integrable_exp_mul_abs (ht_int_pos : Integrable (fun œâ ‚Ü¶ rexp (t * X œâ)) Œº)
    (ht_int_neg : Integrable (fun œâ ‚Ü¶ rexp (- t * X œâ)) Œº) :
    Integrable (fun œâ ‚Ü¶ rexp (t * |X œâ|)) Œº := by
  have h := integrable_exp_mul_abs_add (t := t) (Œº := Œº) (X := X) (v := 0) ?_ ?_
  ¬∑ simpa using h
  ¬∑ simpa using ht_int_pos
  ¬∑ simpa using ht_int_neg

lemma integrable_exp_abs_mul_abs_add (ht_int_pos : Integrable (fun œâ ‚Ü¶ rexp ((v + t) * X œâ)) Œº)
    (ht_int_neg : Integrable (fun œâ ‚Ü¶ rexp ((v - t) * X œâ)) Œº) :
    Integrable (fun œâ ‚Ü¶ rexp (|t| * |X œâ| + v * X œâ)) Œº := by
  rcases le_total 0 t with ht_nonneg | ht_nonpos
  ¬∑ simp_rw [abs_of_nonneg ht_nonneg]
    exact integrable_exp_mul_abs_add ht_int_pos ht_int_neg
  ¬∑ simp_rw [abs_of_nonpos ht_nonpos]
    exact integrable_exp_mul_abs_add ht_int_neg (by simpa using ht_int_pos)

/-- If `œâ ‚Ü¶ rexp (t * X œâ)` is integrable at `t` and `-t`, then `œâ ‚Ü¶ rexp (|t| * |X œâ|)` is
integrable. -/
lemma integrable_exp_abs_mul_abs (ht_int_pos : Integrable (fun œâ ‚Ü¶ rexp (t * X œâ)) Œº)
    (ht_int_neg : Integrable (fun œâ ‚Ü¶ rexp (- t * X œâ)) Œº) :
    Integrable (fun œâ ‚Ü¶ rexp (|t| * |X œâ|)) Œº := by
  rcases le_total 0 t with ht_nonneg | ht_nonpos
  ¬∑ simp_rw [abs_of_nonneg ht_nonneg]
    exact integrable_exp_mul_abs ht_int_pos ht_int_neg
  ¬∑ simp_rw [abs_of_nonpos ht_nonpos]
    exact integrable_exp_mul_abs ht_int_neg (by simpa using ht_int_pos)

lemma integrable_pow_abs_mul_exp_of_integrable_exp_mul (ht : t ‚â† 0)
    (ht_int_pos : Integrable (fun x ‚Ü¶ rexp ((v + t) * X x)) Œº)
    (ht_int_neg : Integrable (fun x ‚Ü¶ rexp ((v - t) * X x)) Œº) (n : ‚Ñï) :
    Integrable (fun œâ ‚Ü¶ |X œâ| ^ n * exp (v * X œâ)) Œº := by
  suffices Integrable (fun œâ ‚Ü¶ (t * |X œâ|) ^ n / n.factorial * exp (v * X œâ)) Œº by
    have h_eq œâ : |X œâ| ^ n * exp (v * X œâ)
        = ((t * |X œâ|) ^ n / n.factorial * exp (v * X œâ)) * n.factorial / t ^ n := by
      rw [mul_pow]
      field_simp
      ring
    simp_rw [h_eq]
    exact (this.mul_const _).div_const _
  have h_le œâ : (|t| * |X œâ|) ^ n / n.factorial ‚â§ exp (|t| * |X œâ|) :=
    pow_div_factorial_le_exp _ (by positivity) _
  have h_int := integrable_exp_abs_mul_abs_add ht_int_pos ht_int_neg
  refine Integrable.mono h_int ?_ (ae_of_all _ fun œâ ‚Ü¶ ?_)
  ¬∑ have hX : AEMeasurable X Œº := by
      by_cases hvt : v + t = 0
      ¬∑ have hvt' : v - t ‚â† 0 := by
          rw [sub_ne_zero]
          refine fun h_eq ‚Ü¶ ht ?_
          simpa [h_eq] using hvt
        exact aemeasurable_of_aemeasurable_exp_mul hvt' ht_int_neg.1.aemeasurable
      ¬∑ exact aemeasurable_of_aemeasurable_exp_mul hvt ht_int_pos.1.aemeasurable
    simp_rw [mul_pow]
    refine AEMeasurable.aestronglyMeasurable ?_
    exact (((hX.abs.pow_const _).const_mul _).div_const _).mul
      (measurable_exp.comp_aemeasurable (hX.const_mul _))
  ¬∑ simp only [norm_div, norm_pow, norm_mul, norm_eq_abs, abs_abs, norm_natCast, abs_exp,
      Nat.abs_cast]
    rw [exp_add]
    gcongr
    exact h_le _

/-- If `œâ ‚Ü¶ rexp (t * X œâ)` is integrable at `t` and `-t` for `t ‚â† 0`, then `œâ ‚Ü¶ |X œâ| ^ n` is
integrable for all `n : ‚Ñï`. That is, all moments of `X` are finite. -/
lemma integrable_pow_abs_of_integrable_exp_mul (ht : t ‚â† 0)
    (ht_int_pos : Integrable (fun x ‚Ü¶ rexp (t * X x)) Œº)
    (ht_int_neg : Integrable (fun x ‚Ü¶ rexp (- t * X x)) Œº) (n : ‚Ñï) :
    Integrable (fun œâ ‚Ü¶ |X œâ| ^ n) Œº := by
  have h := integrable_pow_abs_mul_exp_of_integrable_exp_mul (Œº := Œº) (X := X) ht (v := 0) ?_ ?_ n
  ¬∑ simpa using h
  ¬∑ simpa using ht_int_pos
  ¬∑ simpa using ht_int_neg

lemma integrable_pow_mul_exp_of_integrable_exp_mul (ht : t ‚â† 0)
    (ht_int_pos : Integrable (fun x ‚Ü¶ rexp ((v + t) * X x)) Œº)
    (ht_int_neg : Integrable (fun x ‚Ü¶ rexp ((v - t) * X x)) Œº) (n : ‚Ñï) :
    Integrable (fun œâ ‚Ü¶ X œâ ^ n * exp (v * X œâ)) Œº := by
  rw [‚Üê integrable_norm_iff]
  ¬∑ simp_rw [norm_eq_abs, abs_mul, abs_pow, abs_exp]
    exact integrable_pow_abs_mul_exp_of_integrable_exp_mul ht ht_int_pos ht_int_neg n
  ¬∑ have hX : AEMeasurable X Œº := by
      by_cases hvt : v + t = 0
      ¬∑ have hvt' : v - t ‚â† 0 := by
          rw [sub_ne_zero]
          refine fun h_eq ‚Ü¶ ht ?_
          simpa [h_eq] using hvt
        exact aemeasurable_of_aemeasurable_exp_mul hvt' ht_int_neg.1.aemeasurable
      ¬∑ exact aemeasurable_of_aemeasurable_exp_mul hvt ht_int_pos.1.aemeasurable
    exact ((hX.pow_const _).mul
      (measurable_exp.comp_aemeasurable (hX.const_mul _))).aestronglyMeasurable

/-- If `œâ ‚Ü¶ rexp (t * X œâ)` is integrable at `t` and `-t` for `t ‚â† 0`, then `œâ ‚Ü¶ X œâ ^ n` is
integrable for all `n : ‚Ñï`. -/
lemma integrable_pow_of_integrable_exp_mul (ht : t ‚â† 0)
    (ht_int_pos : Integrable (fun x ‚Ü¶ rexp (t * X x)) Œº)
    (ht_int_neg : Integrable (fun x ‚Ü¶ rexp (- t * X x)) Œº) (n : ‚Ñï) :
    Integrable (fun œâ ‚Ü¶ X œâ ^ n) Œº := by
  have h := integrable_pow_mul_exp_of_integrable_exp_mul (Œº := Œº) (X := X) ht (v := 0) ?_ ?_ n
  ¬∑ simpa using h
  ¬∑ simpa using ht_int_pos
  ¬∑ simpa using ht_int_neg

end Integrable

/-- Moment generating function of a real random variable `X`: `fun t => Œº[exp(t*X)]`. -/
def mgf (X : Œ© ‚Üí ‚Ñù) (Œº : Measure Œ©) (t : ‚Ñù) : ‚Ñù :=
  Œº[fun œâ => exp (t * X œâ)]

/-- Cumulant generating function of a real random variable `X`: `fun t => log Œº[exp(t*X)]`. -/
def cgf (X : Œ© ‚Üí ‚Ñù) (Œº : Measure Œ©) (t : ‚Ñù) : ‚Ñù :=
  log (mgf X Œº t)

@[simp]
theorem mgf_zero_fun : mgf 0 Œº t = (Œº Set.univ).toReal := by
  simp only [mgf, Pi.zero_apply, mul_zero, exp_zero, integral_const, smul_eq_mul, mul_one]

@[simp]
theorem cgf_zero_fun : cgf 0 Œº t = log (Œº Set.univ).toReal := by simp only [cgf, mgf_zero_fun]

@[simp]
theorem mgf_zero_measure : mgf X (0 : Measure Œ©) t = 0 := by simp only [mgf, integral_zero_measure]

@[simp]
theorem cgf_zero_measure : cgf X (0 : Measure Œ©) t = 0 := by
  simp only [cgf, log_zero, mgf_zero_measure]

@[simp]
theorem mgf_const' (c : ‚Ñù) : mgf (fun _ => c) Œº t = (Œº Set.univ).toReal * exp (t * c) := by
  simp only [mgf, integral_const, smul_eq_mul]

-- @[simp] -- Porting note: `simp only` already proves this
theorem mgf_const (c : ‚Ñù) [IsProbabilityMeasure Œº] : mgf (fun _ => c) Œº t = exp (t * c) := by
  simp only [mgf_const', measure_univ, ENNReal.one_toReal, one_mul]

@[simp]
theorem cgf_const' [IsFiniteMeasure Œº] (hŒº : Œº ‚â† 0) (c : ‚Ñù) :
    cgf (fun _ => c) Œº t = log (Œº Set.univ).toReal + t * c := by
  simp only [cgf, mgf_const']
  rw [log_mul _ (exp_pos _).ne']
  ¬∑ rw [log_exp _]
  ¬∑ rw [Ne, ENNReal.toReal_eq_zero_iff, Measure.measure_univ_eq_zero]
    simp only [hŒº, measure_ne_top Œº Set.univ, or_self_iff, not_false_iff]

@[simp]
theorem cgf_const [IsProbabilityMeasure Œº] (c : ‚Ñù) : cgf (fun _ => c) Œº t = t * c := by
  simp only [cgf, mgf_const, log_exp]

@[simp]
theorem mgf_zero' : mgf X Œº 0 = (Œº Set.univ).toReal := by
  simp only [mgf, zero_mul, exp_zero, integral_const, smul_eq_mul, mul_one]

-- @[simp] -- Porting note: `simp only` already proves this
theorem mgf_zero [IsProbabilityMeasure Œº] : mgf X Œº 0 = 1 := by
  simp only [mgf_zero', measure_univ, ENNReal.one_toReal]

theorem cgf_zero' : cgf X Œº 0 = log (Œº Set.univ).toReal := by simp only [cgf, mgf_zero']

@[simp]
theorem cgf_zero [IsZeroOrProbabilityMeasure Œº] : cgf X Œº 0 = 0 := by
  rcases eq_zero_or_isProbabilityMeasure Œº with rfl | h <;> simp [cgf_zero']

theorem mgf_undef (hX : ¬¨Integrable (fun œâ => exp (t * X œâ)) Œº) : mgf X Œº t = 0 := by
  simp only [mgf, integral_undef hX]

theorem cgf_undef (hX : ¬¨Integrable (fun œâ => exp (t * X œâ)) Œº) : cgf X Œº t = 0 := by
  simp only [cgf, mgf_undef hX, log_zero]

theorem mgf_nonneg : 0 ‚â§ mgf X Œº t := by
  unfold mgf; positivity

theorem mgf_pos' (hŒº : Œº ‚â† 0) (h_int_X : Integrable (fun œâ => exp (t * X œâ)) Œº) :
    0 < mgf X Œº t := by
  simp_rw [mgf]
  have : ‚à´ x : Œ©, exp (t * X x) ‚àÇŒº = ‚à´ x : Œ© in Set.univ, exp (t * X x) ‚àÇŒº := by
    simp only [Measure.restrict_univ]
  rw [this, setIntegral_pos_iff_support_of_nonneg_ae _ _]
  ¬∑ have h_eq_univ : (Function.support fun x : Œ© => exp (t * X x)) = Set.univ := by
      ext1 x
      simp only [Function.mem_support, Set.mem_univ, iff_true]
      exact (exp_pos _).ne'
    rw [h_eq_univ, Set.inter_univ _]
    refine Ne.bot_lt ?_
    simp only [hŒº, ENNReal.bot_eq_zero, Ne, Measure.measure_univ_eq_zero, not_false_iff]
  ¬∑ filter_upwards with x
    rw [Pi.zero_apply]
    exact (exp_pos _).le
  ¬∑ rwa [integrableOn_univ]

theorem mgf_pos [IsProbabilityMeasure Œº] (h_int_X : Integrable (fun œâ => exp (t * X œâ)) Œº) :
    0 < mgf X Œº t :=
  mgf_pos' (IsProbabilityMeasure.ne_zero Œº) h_int_X

/-- `exp cgf = mgf`.
For a version for probability measures, without the hypothesis `hŒº : Œº ‚â† 0`, see `exp_cgf`. -/
lemma exp_cgf' (hŒº : Œº ‚â† 0) (h_int_X : Integrable (fun œâ => exp (t * X œâ)) Œº) :
    exp (cgf X Œº t) = mgf X Œº t := by rw [cgf, exp_log (mgf_pos' hŒº h_int_X)]

/-- `exp cgf = mgf`.
For a version that works more generally than probability measures, with a hypothesis `hŒº : Œº ‚â† 0`,
see `exp_cgf'`. -/
lemma exp_cgf [IsProbabilityMeasure Œº] (h_int_X : Integrable (fun œâ => exp (t * X œâ)) Œº) :
    exp (cgf X Œº t) = mgf X Œº t := by rw [cgf, exp_log (mgf_pos h_int_X)]

theorem mgf_neg : mgf (-X) Œº t = mgf X Œº (-t) := by simp_rw [mgf, Pi.neg_apply, mul_neg, neg_mul]

theorem cgf_neg : cgf (-X) Œº t = cgf X Œº (-t) := by simp_rw [cgf, mgf_neg]

theorem mgf_smul_left (Œ± : ‚Ñù) : mgf (Œ± ‚Ä¢ X) Œº t = mgf X Œº (Œ± * t) := by
  simp_rw [mgf, Pi.smul_apply, smul_eq_mul, mul_comm Œ± t, mul_assoc]

/-- The moment generating function is monotone in the random variable for `t ‚â• 0`. -/
lemma mgf_mono_of_nonneg {Y : Œ© ‚Üí ‚Ñù} (hXY : X ‚â§·µê[Œº] Y) (ht : 0 ‚â§ t)
    (htY : Integrable (fun œâ ‚Ü¶ exp (t * Y œâ)) Œº) :
    mgf X Œº t ‚â§ mgf Y Œº t := by
  by_cases htX : Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) Œº
  ¬∑ refine integral_mono_ae htX htY ?_
    filter_upwards [hXY] with œâ hœâ using by gcongr
  ¬∑ rw [mgf_undef htX]
    exact mgf_nonneg

/-- The moment generating function is antitone in the random variable for `t ‚â§ 0`. -/
lemma mgf_anti_of_nonpos {Y : Œ© ‚Üí ‚Ñù} (hXY : X ‚â§·µê[Œº] Y) (ht : t ‚â§ 0)
    (htX : Integrable (fun œâ ‚Ü¶ exp (t * X œâ)) Œº) :
    mgf Y Œº t ‚â§ mgf X Œº t := by
  by_cases htY : Integrable (fun œâ ‚Ü¶ exp (t * Y œâ)) Œº
  ¬∑ refine integral_mono_ae htY htX ?_
    filter_upwards [hXY] with œâ hœâ using exp_monotone <| mul_le_mul_of_nonpos_left hœâ ht
  ¬∑ rw [mgf_undef htY]
    exact mgf_nonneg

lemma mgf_abs_le_add (ht_int_pos : Integrable (fun œâ ‚Ü¶ rexp (t * X œâ)) Œº)
    (ht_int_neg : Integrable (fun œâ ‚Ü¶ rexp (- t * X œâ)) Œº) :
    mgf (fun œâ ‚Ü¶ |X œâ|) Œº t ‚â§ mgf X Œº t + mgf (-X) Œº t := by
  simp_rw [mgf]
  rw [‚Üê integral_add ht_int_pos (by simpa using ht_int_neg)]
  have h_int_add : Integrable (fun a ‚Ü¶ rexp (t * X a) + rexp (-(t * X a))) Œº :=
    ht_int_pos.add <| by simpa using ht_int_neg
  simp only [Pi.neg_apply, mul_neg, ge_iff_le]
  refine integral_mono_ae ?_ h_int_add
    (ae_of_all _ (fun œâ ‚Ü¶ exp_mul_abs_le_add (t := t) (u := X œâ)))
  exact integrable_exp_mul_abs ht_int_pos ht_int_neg

lemma summable_integral_abs_mul_exp_of_todo
    (ht_int_pos : Integrable (fun œâ ‚Ü¶ rexp ((v + t) * X œâ)) Œº)
    (ht_int_neg : Integrable (fun œâ ‚Ü¶ rexp ((v - t) * X œâ)) Œº) :
    Summable fun (i : ‚Ñï) ‚Ü¶ Œº[fun œâ ‚Ü¶ |X œâ| ^ i / i.factorial * |t| ^ i * exp (v * X œâ)] := by
  by_cases ht : t = 0
  ¬∑ simp only [ht, abs_zero]
    refine summable_of_ne_finset_zero (s := {0}) (fun n hn ‚Ü¶ ?_)
    rw [zero_pow]
    ¬∑ simp
    ¬∑ simpa using hn
  suffices Summable fun i ‚Ü¶ ‚à´ œâ, (|t| * |X œâ|) ^ i / i.factorial * exp (v * X œâ) ‚àÇŒº by
    simp_rw [mul_pow] at this
    convert this using 4 with i œâ
    ring
  have h_int (u : ‚Ñù) (i : ‚Ñï) :
      Integrable (fun œâ ‚Ü¶ (u * |X œâ|) ^ i / i.factorial * exp (v * X œâ)) Œº := by
    simp_rw [mul_pow]
    simp_rw [mul_comm _ (exp (v * _)), mul_div]
    refine Integrable.div_const ?_ _
    simp_rw [mul_comm (exp _), mul_assoc]
    refine Integrable.const_mul ?_ _
    exact integrable_pow_abs_mul_exp_of_integrable_exp_mul ht ht_int_pos ht_int_neg i
  refine summable_of_sum_range_le (c := Œº[fun œâ ‚Ü¶ exp (|t| * |X œâ| + v * X œâ)])
    (fun _ ‚Ü¶ integral_nonneg fun œâ ‚Ü¶ by positivity) fun n ‚Ü¶ ?_
  rw [‚Üê integral_finset_sum]
  ¬∑ refine integral_mono ?_ ?_ ?_
    ¬∑ exact integrable_finset_sum (range n) fun i a ‚Ü¶ h_int |t| i
    ¬∑ exact integrable_exp_abs_mul_abs_add ht_int_pos ht_int_neg
    ¬∑ intro œâ
      simp only
      rw [‚Üê sum_mul, exp_add]
      gcongr
      exact sum_le_exp_of_nonneg (by positivity) _
  ¬∑ exact fun i _ ‚Ü¶ h_int _ i

lemma summable_integral_abs_of_todo (ht_int_pos : Integrable (fun œâ ‚Ü¶ rexp (t * X œâ)) Œº)
    (ht_int_neg : Integrable (fun œâ ‚Ü¶ rexp (- t * X œâ)) Œº) :
    Summable fun (i : ‚Ñï) ‚Ü¶ Œº[fun œâ ‚Ü¶ |X œâ| ^ i / i.factorial * |t| ^ i] := by
  have h := summable_integral_abs_mul_exp_of_todo (Œº := Œº) (X := X) (v := 0) (t := t) ?_ ?_
  ¬∑ simpa using h
  ¬∑ simpa using ht_int_pos
  ¬∑ simpa using ht_int_neg

lemma summable_integral_pow_abs_mul_exp_mul_abs
    (ht_int_pos : Integrable (fun œâ ‚Ü¶ rexp ((v + t) * X œâ)) Œº)
    (ht_int_neg : Integrable (fun œâ ‚Ü¶ rexp ((v - t) * X œâ)) Œº) :
    Summable fun (i : ‚Ñï) ‚Ü¶ Œº[fun œâ ‚Ü¶ |X œâ| ^ i * exp (v * X œâ)] / i.factorial * |t| ^ i := by
  simp_rw [‚Üê integral_div, ‚Üê integral_mul_right]
  have h_eq i œâ : |X œâ| ^ i * rexp (v * X œâ) / i.factorial * |t| ^ i
      = |X œâ| ^ i / i.factorial * |t| ^ i * rexp (v * X œâ) := by ring
  simp_rw [h_eq]
  exact summable_integral_abs_mul_exp_of_todo ht_int_pos ht_int_neg

lemma summable_integral_pow_abs_mul_abs (ht_int_pos : Integrable (fun œâ ‚Ü¶ rexp (t * X œâ)) Œº)
    (ht_int_neg : Integrable (fun œâ ‚Ü¶ rexp (- t * X œâ)) Œº) :
    Summable fun (i : ‚Ñï) ‚Ü¶ Œº[fun œâ ‚Ü¶ |X œâ| ^ i] / i.factorial * |t| ^ i := by
  simp_rw [‚Üê integral_div, ‚Üê integral_mul_right]
  exact summable_integral_abs_of_todo ht_int_pos ht_int_neg

lemma summable_integral_pow_mul_exp_mul
    (ht_int_pos : Integrable (fun œâ ‚Ü¶ rexp ((v + t) * X œâ)) Œº)
    (ht_int_neg : Integrable (fun œâ ‚Ü¶ rexp ((v - t) * X œâ)) Œº) :
    Summable fun (i : ‚Ñï) ‚Ü¶ Œº[fun œâ ‚Ü¶ X œâ ^ i * rexp (v * X œâ)] / i.factorial * t ^ i := by
  refine (summable_integral_pow_abs_mul_exp_mul_abs ht_int_pos ht_int_neg).mono fun i ‚Ü¶ ?_
  simp only [Pi.pow_apply, norm_mul, norm_div, norm_eq_abs, norm_natCast, norm_pow, abs_abs,
    Nat.abs_cast]
  refine mul_le_mul ?_ le_rfl (by positivity) (by positivity)
  rw [div_le_div_iff_of_pos_right (by positivity)]
  conv_rhs => rw [abs_of_nonneg (integral_nonneg (fun _ ‚Ü¶ by positivity))]
  simp_rw [‚Üê norm_eq_abs]
  refine (norm_integral_le_integral_norm _).trans ?_
  simp

lemma summable_integral_pow_mul (ht_int_pos : Integrable (fun œâ ‚Ü¶ rexp (t * X œâ)) Œº)
    (ht_int_neg : Integrable (fun œâ ‚Ü¶ rexp (- t * X œâ)) Œº) :
    Summable fun (i : ‚Ñï) ‚Ü¶ Œº[X ^ i] / i.factorial * t ^ i := by
  have h := summable_integral_pow_mul_exp_mul (Œº := Œº) (X := X) (v := 0) (t := t) ?_ ?_
  ¬∑ simpa using h
  ¬∑ simpa using ht_int_pos
  ¬∑ simpa using ht_int_neg

lemma mgf_abs_eq_tsum (ht_int_pos : Integrable (fun œâ ‚Ü¶ rexp (t * X œâ)) Œº)
    (ht_int_neg : Integrable (fun œâ ‚Ü¶ rexp (- t * X œâ)) Œº) :
    mgf (fun œâ ‚Ü¶ |X œâ|) Œº t = ‚àë' n, (Œº[fun œâ ‚Ü¶ |X œâ| ^ n]) / n.factorial * t ^ n := by
  by_cases ht : t = 0
  ¬∑ rw [tsum_eq_single 0]
    ¬∑ simp [ht]
    ¬∑ intro n hn
      simp [zero_pow hn, ht]
  simp_rw [mgf, exp_eq_tsum]
  have h_int (u : ‚Ñù) (i : ‚Ñï) : Integrable (fun œâ ‚Ü¶ (u * |X œâ|) ^ i / i.factorial) Œº := by
    refine Integrable.div_const ?_ _
    simp_rw [mul_pow]
    refine Integrable.const_mul ?_ _
    exact integrable_pow_abs_of_integrable_exp_mul ht ht_int_pos ht_int_neg i
  rw [‚Üê integral_tsum_of_summable_integral_norm (h_int _)]
  ¬∑ congr with n
    simp_rw [integral_div, mul_pow, integral_mul_left]
    ring
  ¬∑ simp only [norm_div, norm_pow, norm_mul, norm_eq_abs, abs_abs, norm_natCast, Nat.abs_cast]
    convert summable_integral_abs_of_todo ht_int_pos ht_int_neg with i œâ
    ring

lemma mgf_add_eq_tsum (ht_int_pos : Integrable (fun œâ ‚Ü¶ rexp ((v + t) * X œâ)) Œº)
    (ht_int_neg : Integrable (fun œâ ‚Ü¶ rexp ((v - t) * X œâ)) Œº) :
    mgf X Œº (v + t) = ‚àë' n, Œº[fun œâ ‚Ü¶ X œâ ^ n * exp (v * X œâ)] / n.factorial * t ^ n := by
  by_cases ht : t = 0
  ¬∑ rw [tsum_eq_single 0]
    ¬∑ simp [ht, mgf]
    ¬∑ intro n hn
      simp [zero_pow hn, ht]
  have h_int_pow i : Integrable (fun œâ ‚Ü¶ X œâ ^ i / i.factorial * t ^ i * exp (v * X œâ)) Œº := by
    simp_rw [mul_comm _ (exp _), ‚Üê mul_assoc, ‚Üê mul_div_assoc, mul_comm (exp _)]
    refine (Integrable.div_const ?_ _).mul_const _
    exact integrable_pow_mul_exp_of_integrable_exp_mul ht ht_int_pos ht_int_neg i
  suffices Tendsto (fun n ‚Ü¶ |mgf X Œº (v + t)
        - Œº[fun œâ ‚Ü¶ ‚àë i in range n, X œâ ^ i / i.factorial * t ^ i * exp (v * X œâ)]|)
      atTop (ùìù 0) by
    change Tendsto (abs ‚àò _) _ _ at this
    rw [‚Üê tendsto_zero_iff_abs_tendsto_zero] at this
    have h_eq n : Œº[fun œâ ‚Ü¶ ‚àë i ‚àà range n, X œâ ^ i / i.factorial * t ^ i * exp (v * X œâ)]
        = ‚àë i ‚àà range n, Œº[fun œâ ‚Ü¶ X œâ ^ i * exp (v * X œâ)] / i.factorial * t ^ i := by
      rw [integral_finset_sum]
      ¬∑ congr with n
        rw [‚Üê integral_div, ‚Üê integral_mul_right]
        congr with œâ
        ring
      ¬∑ exact fun i _ ‚Ü¶ h_int_pow i
    simp_rw [h_eq] at this
    suffices Tendsto (fun n ‚Ü¶
          ‚àë i ‚àà range n, Œº[fun œâ ‚Ü¶ X œâ ^ i * exp (v * X œâ)] / i.factorial * t ^ i)
        atTop (ùìù (mgf X Œº (v + t))) by
      refine tendsto_nhds_unique this ?_
      refine HasSum.Multipliable.tendsto_sum_tsum_nat ?_
      exact summable_integral_pow_mul_exp_mul ht_int_pos ht_int_neg
    rwa [‚Üê tendsto_const_sub_iff (b := mgf X Œº (v + t)), sub_self]
  have h_le n : |mgf X Œº (v + t)
        - Œº[fun œâ ‚Ü¶ ‚àë i ‚àà range n, X œâ ^ i / ‚Üëi.factorial * t ^ i * exp (v * X œâ)]|
      ‚â§ ‚àë' i : {j // j ‚àâ range n},
        Œº[fun œâ ‚Ü¶ |X œâ| ^ (i : ‚Ñï) * exp (v * X œâ)] / (i : ‚Ñï).factorial * |t| ^ (i : ‚Ñï) := by
    calc |mgf X Œº (v + t)
        - Œº[fun œâ ‚Ü¶ ‚àë i ‚àà range n, X œâ ^ i / ‚Üëi.factorial * t ^ i * exp (v * X œâ)]|
    _ = |Œº[fun œâ ‚Ü¶ ‚àë' i : {j // j ‚àâ range n},
        X œâ ^ (i : ‚Ñï) / (i : ‚Ñï).factorial * t ^ (i : ‚Ñï) * exp (v * X œâ)]| := by
      simp_rw [mgf]
      rw [‚Üê integral_sub ht_int_pos (integrable_finset_sum _ (fun i _ ‚Ü¶ h_int_pow i))]
      congr with œâ
      rw [add_mul, add_comm, exp_add]
      rw [exp_eq_tsum, sub_eq_iff_eq_add']
      have : ‚àë' n, (t * X œâ) ^ n / n.factorial
          = ‚àë' n, X œâ ^ n / n.factorial * t ^ n := by
        simp_rw [mul_pow]
        congr with n
        ring
      rw [this, ‚Üê tsum_mul_right]
      symm
      refine sum_add_tsum_compl ?_
      suffices Summable fun i ‚Ü¶ (t * X œâ) ^ i / i.factorial * exp (v * X œâ) by
        convert this using 2 with i
        ring
      refine Summable.mul_right _ ?_
      exact summable_pow_div_factorial _
    _ = |‚àë' i : {j // j ‚àâ range n},
        Œº[fun œâ ‚Ü¶ X œâ ^ (i : ‚Ñï) * exp (v * X œâ)] / (i : ‚Ñï).factorial * t ^ (i : ‚Ñï)| := by
      have h_eq i œâ : X œâ ^ i / ‚Üëi.factorial * t ^ i * exp (v * X œâ)
          = X œâ ^ i * exp (v * X œâ) / ‚Üëi.factorial * t ^ i := by ring
      simp_rw [h_eq]
      rw [‚Üê integral_tsum_of_summable_integral_norm]
      ¬∑ congr with i
        rw [‚Üê integral_div, ‚Üê integral_mul_right]
      ¬∑ refine fun i ‚Ü¶ (Integrable.div_const ?_ _).mul_const _
        exact integrable_pow_mul_exp_of_integrable_exp_mul ht ht_int_pos ht_int_neg _
      ¬∑ simp only [norm_mul, norm_div, norm_pow, norm_eq_abs, norm_natCast, Nat.abs_cast]
        simp_rw [integral_mul_right, integral_div, abs_exp]
        exact (summable_integral_pow_abs_mul_exp_mul_abs ht_int_pos ht_int_neg).subtype (range n)·∂ú
    _ ‚â§ ‚àë' i : {j // j ‚àâ range n},
        |Œº[fun œâ ‚Ü¶ X œâ ^ (i : ‚Ñï) * exp (v * X œâ)] / (i : ‚Ñï).factorial * t ^ (i : ‚Ñï)| := by
      simp_rw [‚Üê norm_eq_abs]
      refine norm_tsum_le_tsum_norm ?_
      rw [summable_norm_iff]
      exact (summable_integral_pow_mul_exp_mul ht_int_pos ht_int_neg).subtype (range n)·∂ú
    _ ‚â§ ‚àë' i : {j // j ‚àâ range n},
          Œº[fun œâ ‚Ü¶ |X œâ| ^ (i : ‚Ñï) * exp (v * X œâ)] / (i : ‚Ñï).factorial * |t| ^ (i : ‚Ñï) := by
      simp only [Pi.pow_apply]
      refine tsum_mono ?_ ?_ fun n ‚Ü¶ ?_
      ¬∑ rw [summable_abs_iff]
        exact (summable_integral_pow_mul_exp_mul ht_int_pos ht_int_neg).subtype (range n)·∂ú
      ¬∑ exact (summable_integral_pow_abs_mul_exp_mul_abs ht_int_pos ht_int_neg).subtype (range n)·∂ú
      ¬∑ simp only
        rw [abs_mul, abs_div, Nat.abs_cast, abs_pow]
        gcongr
        simp_rw [‚Üê norm_eq_abs]
        refine (norm_integral_le_integral_norm _).trans ?_
        simp
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le (tendsto_const_nhds) ?_ ?_ h_le
  ¬∑ refine (tendsto_tsum_compl_atTop_zero
      (fun i ‚Ü¶ Œº[fun œâ ‚Ü¶ |X œâ| ^ (i : ‚Ñï) * exp (v * X œâ)]
        / (i : ‚Ñï).factorial * |t| ^ (i : ‚Ñï))).comp ?_
    exact tendsto_finset_range
  ¬∑ intro n
    positivity

lemma mgf_eq_tsum (ht_int_pos : Integrable (fun œâ ‚Ü¶ rexp (t * X œâ)) Œº)
    (ht_int_neg : Integrable (fun œâ ‚Ü¶ rexp (- t * X œâ)) Œº) :
    mgf X Œº t = ‚àë' n, Œº[X ^ n] / n.factorial * t ^ n := by
  have h := mgf_add_eq_tsum (Œº := Œº) (X := X) (v := 0) (t := t) ?_ ?_
  ¬∑ simpa using h
  ¬∑ simpa using ht_int_pos
  ¬∑ simpa using ht_int_neg

lemma hasFPowerSeriesOnBall_mgf' [IsFiniteMeasure Œº] (ht : t ‚â† 0)
    (ht_int_pos : Integrable (fun œâ ‚Ü¶ rexp ((v + t) * X œâ)) Œº)
    (ht_int_neg : Integrable (fun œâ ‚Ü¶ rexp ((v - t) * X œâ)) Œº) :
    HasFPowerSeriesOnBall (mgf X Œº)
      (FormalMultilinearSeries.ofScalars ‚Ñù
        (fun n ‚Ü¶ (Œº[fun œâ ‚Ü¶ X œâ ^ n * exp (v * X œâ)] : ‚Ñù) / n.factorial)) v ‚Äñt‚Äñ‚Çä := by
  constructor
  ¬∑ refine FormalMultilinearSeries.le_radius_of_summable _ ?_
    simp only [Pi.pow_apply, FormalMultilinearSeries.ofScalars_norm, norm_eq_abs,
      coe_nnnorm, abs_div, Nat.abs_cast]
    have h := summable_integral_pow_mul_exp_mul ht_int_pos ht_int_neg
    rw [‚Üê summable_abs_iff] at h
    simp_rw [abs_mul, abs_div, abs_pow, Nat.abs_cast] at h
    exact h
  ¬∑ simp [ht]
  ¬∑ intro y hy
    simp_rw [FormalMultilinearSeries.ofScalars_apply_eq]
    simp only [Pi.pow_apply, smul_eq_mul, zero_add]
    simp only [Metric.emetric_ball_nnreal, coe_nnnorm, norm_eq_abs, Metric.mem_ball,
      dist_zero_right] at hy
    have hy_int_pos : Integrable (fun œâ ‚Ü¶ rexp ((v + y) * X œâ)) Œº := by
      rcases le_total 0 t with ht | ht
      ¬∑ refine integrable_exp_mul_of_le_of_le ht_int_neg ht_int_pos ?_ ?_
        ¬∑ rw [sub_eq_add_neg]
          gcongr
          sorry
        ¬∑ gcongr
          sorry
      ¬∑ refine integrable_exp_mul_of_le_of_le ht_int_pos ht_int_neg ?_ ?_
        ¬∑ gcongr
          sorry
        ¬∑ rw [sub_eq_add_neg]
          gcongr
          sorry
    have hy_int_neg : Integrable (fun œâ ‚Ü¶ rexp ((v - y) * X œâ)) Œº := by
      sorry
    rw [Summable.hasSum_iff]
    ¬∑ exact (mgf_add_eq_tsum hy_int_pos hy_int_neg).symm
    ¬∑ exact summable_integral_pow_mul_exp_mul hy_int_pos hy_int_neg

lemma hasFPowerSeriesOnBall_mgf [IsFiniteMeasure Œº] (ht : t ‚â† 0)
    (ht_int_pos : Integrable (fun œâ ‚Ü¶ rexp (t * X œâ)) Œº)
    (ht_int_neg : Integrable (fun œâ ‚Ü¶ rexp (-t * X œâ)) Œº) :
    HasFPowerSeriesOnBall (mgf X Œº)
      (FormalMultilinearSeries.ofScalars ‚Ñù (fun n ‚Ü¶ (Œº[X ^ n] : ‚Ñù) / n.factorial)) 0 ‚Äñt‚Äñ‚Çä := by
  constructor
  ¬∑ refine FormalMultilinearSeries.le_radius_of_summable _ ?_
    simp only [Pi.pow_apply, FormalMultilinearSeries.ofScalars_norm, norm_eq_abs,
      coe_nnnorm, abs_div, Nat.abs_cast]
    have h := summable_integral_pow_mul ht_int_pos ht_int_neg
    rw [‚Üê summable_abs_iff] at h
    simp_rw [abs_mul, abs_div, abs_pow, Nat.abs_cast, Pi.pow_apply] at h
    exact h
  ¬∑ simp [ht]
  ¬∑ intro y hy
    simp_rw [FormalMultilinearSeries.ofScalars_apply_eq]
    simp only [Pi.pow_apply, smul_eq_mul, zero_add]
    simp only [Metric.emetric_ball_nnreal, coe_nnnorm, norm_eq_abs, Metric.mem_ball,
      dist_zero_right] at hy
    have hy_int_pos : Integrable (fun œâ ‚Ü¶ rexp (y * X œâ)) Œº :=
      integrable_exp_mul_of_abs_le ht_int_pos ht_int_neg hy.le
    have hy_int_neg : Integrable (fun œâ ‚Ü¶ rexp (- y * X œâ)) Œº := by
      refine integrable_exp_mul_of_abs_le ht_int_pos ht_int_neg ?_
      simp only [abs_neg]
      exact hy.le
    rw [Summable.hasSum_iff]
    ¬∑ exact (mgf_eq_tsum hy_int_pos hy_int_neg).symm
    ¬∑ exact summable_integral_pow_mul hy_int_pos hy_int_neg

lemma hasFPowerSeriesAt_mgf' [IsFiniteMeasure Œº] (ht : t ‚â† 0)
    (ht_int_pos : Integrable (fun œâ ‚Ü¶ rexp ((v + t) * X œâ)) Œº)
    (ht_int_neg : Integrable (fun œâ ‚Ü¶ rexp ((v - t) * X œâ)) Œº) :
    HasFPowerSeriesAt (mgf X Œº)
      (FormalMultilinearSeries.ofScalars ‚Ñù
        (fun n ‚Ü¶ (Œº[fun œâ ‚Ü¶ X œâ ^ n * exp (v * X œâ)] : ‚Ñù) / n.factorial)) v :=
  ‚ü®‚Äñt‚Äñ‚Çä, hasFPowerSeriesOnBall_mgf' ht ht_int_pos ht_int_neg‚ü©

lemma hasFPowerSeriesAt_mgf [IsFiniteMeasure Œº] (ht : t ‚â† 0)
    (ht_int_pos : Integrable (fun œâ ‚Ü¶ rexp (t * X œâ)) Œº)
    (ht_int_neg : Integrable (fun œâ ‚Ü¶ rexp (-t * X œâ)) Œº) :
    HasFPowerSeriesAt (mgf X Œº)
      (FormalMultilinearSeries.ofScalars ‚Ñù (fun n ‚Ü¶ (Œº[X ^ n] : ‚Ñù) / n.factorial)) 0 :=
  ‚ü®‚Äñt‚Äñ‚Çä, hasFPowerSeriesOnBall_mgf ht ht_int_pos ht_int_neg‚ü©

lemma analyticAt_mgf' [IsFiniteMeasure Œº] (ht : t ‚â† 0)
    (ht_int_pos : Integrable (fun œâ ‚Ü¶ rexp ((v + t) * X œâ)) Œº)
    (ht_int_neg : Integrable (fun œâ ‚Ü¶ rexp ((v - t) * X œâ)) Œº) :
    AnalyticAt ‚Ñù (mgf X Œº) v :=
  ‚ü®_, hasFPowerSeriesAt_mgf' ht ht_int_pos ht_int_neg‚ü©

lemma analyticAt_mgf [IsFiniteMeasure Œº] (ht : t ‚â† 0)
    (ht_int_pos : Integrable (fun œâ ‚Ü¶ rexp (t * X œâ)) Œº)
    (ht_int_neg : Integrable (fun œâ ‚Ü¶ rexp (-t * X œâ)) Œº) :
    AnalyticAt ‚Ñù (mgf X Œº) 0 :=
  ‚ü®_, hasFPowerSeriesAt_mgf ht ht_int_pos ht_int_neg‚ü©

lemma analyticOnNhd_mgf [IsFiniteMeasure Œº] :
    AnalyticOnNhd ‚Ñù (mgf X Œº) (interior {x | Integrable (fun œâ ‚Ü¶ exp (x * X œâ)) Œº}) := by
  intro x hx
  rw [mem_interior_iff_mem_nhds, mem_nhds_iff_exists_Ioo_subset] at hx
  obtain ‚ü®l, u, hxlu, h_subset‚ü© := hx
  sorry

lemma iteratedDeriv_mgf_zero [IsFiniteMeasure Œº]
    (hu_int_pos : Integrable (fun œâ ‚Ü¶ rexp (u * X œâ)) Œº)
    (hu_int_neg : Integrable (fun œâ ‚Ü¶ rexp (- u * X œâ)) Œº) (n : ‚Ñï) :
    iteratedDeriv n (mgf X Œº) 0 = Œº[X ^ n] := by
  sorry

section IndepFun

/-- This is a trivial application of `IndepFun.comp` but it will come up frequently. -/
theorem IndepFun.exp_mul {X Y : Œ© ‚Üí ‚Ñù} (h_indep : IndepFun X Y Œº) (s t : ‚Ñù) :
    IndepFun (fun œâ => exp (s * X œâ)) (fun œâ => exp (t * Y œâ)) Œº := by
  have h_meas : ‚àÄ t, Measurable fun x => exp (t * x) := fun t => (measurable_id'.const_mul t).exp
  change IndepFun ((fun x => exp (s * x)) ‚àò X) ((fun x => exp (t * x)) ‚àò Y) Œº
  exact IndepFun.comp h_indep (h_meas s) (h_meas t)

theorem IndepFun.mgf_add {X Y : Œ© ‚Üí ‚Ñù} (h_indep : IndepFun X Y Œº)
    (hX : AEStronglyMeasurable (fun œâ => exp (t * X œâ)) Œº)
    (hY : AEStronglyMeasurable (fun œâ => exp (t * Y œâ)) Œº) :
    mgf (X + Y) Œº t = mgf X Œº t * mgf Y Œº t := by
  simp_rw [mgf, Pi.add_apply, mul_add, exp_add]
  exact (h_indep.exp_mul t t).integral_mul hX hY

theorem IndepFun.mgf_add' {X Y : Œ© ‚Üí ‚Ñù} (h_indep : IndepFun X Y Œº) (hX : AEStronglyMeasurable X Œº)
    (hY : AEStronglyMeasurable Y Œº) : mgf (X + Y) Œº t = mgf X Œº t * mgf Y Œº t := by
  have A : Continuous fun x : ‚Ñù => exp (t * x) := by fun_prop
  have h'X : AEStronglyMeasurable (fun œâ => exp (t * X œâ)) Œº :=
    A.aestronglyMeasurable.comp_aemeasurable hX.aemeasurable
  have h'Y : AEStronglyMeasurable (fun œâ => exp (t * Y œâ)) Œº :=
    A.aestronglyMeasurable.comp_aemeasurable hY.aemeasurable
  exact h_indep.mgf_add h'X h'Y

theorem IndepFun.cgf_add {X Y : Œ© ‚Üí ‚Ñù} (h_indep : IndepFun X Y Œº)
    (h_int_X : Integrable (fun œâ => exp (t * X œâ)) Œº)
    (h_int_Y : Integrable (fun œâ => exp (t * Y œâ)) Œº) :
    cgf (X + Y) Œº t = cgf X Œº t + cgf Y Œº t := by
  by_cases hŒº : Œº = 0
  ¬∑ simp [hŒº]
  simp only [cgf, h_indep.mgf_add h_int_X.aestronglyMeasurable h_int_Y.aestronglyMeasurable]
  exact log_mul (mgf_pos' hŒº h_int_X).ne' (mgf_pos' hŒº h_int_Y).ne'

theorem aestronglyMeasurable_exp_mul_add {X Y : Œ© ‚Üí ‚Ñù}
    (h_int_X : AEStronglyMeasurable (fun œâ => exp (t * X œâ)) Œº)
    (h_int_Y : AEStronglyMeasurable (fun œâ => exp (t * Y œâ)) Œº) :
    AEStronglyMeasurable (fun œâ => exp (t * (X + Y) œâ)) Œº := by
  simp_rw [Pi.add_apply, mul_add, exp_add]
  exact AEStronglyMeasurable.mul h_int_X h_int_Y

theorem aestronglyMeasurable_exp_mul_sum {X : Œπ ‚Üí Œ© ‚Üí ‚Ñù} {s : Finset Œπ}
    (h_int : ‚àÄ i ‚àà s, AEStronglyMeasurable (fun œâ => exp (t * X i œâ)) Œº) :
    AEStronglyMeasurable (fun œâ => exp (t * (‚àë i ‚àà s, X i) œâ)) Œº := by
  classical
  induction' s using Finset.induction_on with i s hi_notin_s h_rec h_int
  ¬∑ simp only [Pi.zero_apply, sum_apply, sum_empty, mul_zero, exp_zero]
    exact aestronglyMeasurable_const
  ¬∑ have : ‚àÄ i : Œπ, i ‚àà s ‚Üí AEStronglyMeasurable (fun œâ : Œ© => exp (t * X i œâ)) Œº := fun i hi =>
      h_int i (mem_insert_of_mem hi)
    specialize h_rec this
    rw [sum_insert hi_notin_s]
    apply aestronglyMeasurable_exp_mul_add (h_int i (mem_insert_self _ _)) h_rec

theorem IndepFun.integrable_exp_mul_add {X Y : Œ© ‚Üí ‚Ñù} (h_indep : IndepFun X Y Œº)
    (h_int_X : Integrable (fun œâ => exp (t * X œâ)) Œº)
    (h_int_Y : Integrable (fun œâ => exp (t * Y œâ)) Œº) :
    Integrable (fun œâ => exp (t * (X + Y) œâ)) Œº := by
  simp_rw [Pi.add_apply, mul_add, exp_add]
  exact (h_indep.exp_mul t t).integrable_mul h_int_X h_int_Y

theorem iIndepFun.integrable_exp_mul_sum [IsFiniteMeasure Œº] {X : Œπ ‚Üí Œ© ‚Üí ‚Ñù}
    (h_indep : iIndepFun (fun _ => inferInstance) X Œº) (h_meas : ‚àÄ i, Measurable (X i))
    {s : Finset Œπ} (h_int : ‚àÄ i ‚àà s, Integrable (fun œâ => exp (t * X i œâ)) Œº) :
    Integrable (fun œâ => exp (t * (‚àë i ‚àà s, X i) œâ)) Œº := by
  classical
  induction' s using Finset.induction_on with i s hi_notin_s h_rec h_int
  ¬∑ simp only [Pi.zero_apply, sum_apply, sum_empty, mul_zero, exp_zero]
    exact integrable_const _
  ¬∑ have : ‚àÄ i : Œπ, i ‚àà s ‚Üí Integrable (fun œâ : Œ© => exp (t * X i œâ)) Œº := fun i hi =>
      h_int i (mem_insert_of_mem hi)
    specialize h_rec this
    rw [sum_insert hi_notin_s]
    refine IndepFun.integrable_exp_mul_add ?_ (h_int i (mem_insert_self _ _)) h_rec
    exact (h_indep.indepFun_finset_sum_of_not_mem h_meas hi_notin_s).symm

theorem iIndepFun.mgf_sum {X : Œπ ‚Üí Œ© ‚Üí ‚Ñù}
    (h_indep : iIndepFun (fun _ => inferInstance) X Œº) (h_meas : ‚àÄ i, Measurable (X i))
    (s : Finset Œπ) : mgf (‚àë i ‚àà s, X i) Œº t = ‚àè i ‚àà s, mgf (X i) Œº t := by
  have : IsProbabilityMeasure Œº := h_indep.isProbabilityMeasure
  classical
  induction' s using Finset.induction_on with i s hi_notin_s h_rec h_int
  ¬∑ simp only [sum_empty, mgf_zero_fun, measure_univ, ENNReal.one_toReal, prod_empty]
  ¬∑ have h_int' : ‚àÄ i : Œπ, AEStronglyMeasurable (fun œâ : Œ© => exp (t * X i œâ)) Œº := fun i =>
      ((h_meas i).const_mul t).exp.aestronglyMeasurable
    rw [sum_insert hi_notin_s,
      IndepFun.mgf_add (h_indep.indepFun_finset_sum_of_not_mem h_meas hi_notin_s).symm (h_int' i)
        (aestronglyMeasurable_exp_mul_sum fun i _ => h_int' i),
      h_rec, prod_insert hi_notin_s]

theorem iIndepFun.cgf_sum {X : Œπ ‚Üí Œ© ‚Üí ‚Ñù}
    (h_indep : iIndepFun (fun _ => inferInstance) X Œº) (h_meas : ‚àÄ i, Measurable (X i))
    {s : Finset Œπ} (h_int : ‚àÄ i ‚àà s, Integrable (fun œâ => exp (t * X i œâ)) Œº) :
    cgf (‚àë i ‚àà s, X i) Œº t = ‚àë i ‚àà s, cgf (X i) Œº t := by
  have : IsProbabilityMeasure Œº := h_indep.isProbabilityMeasure
  simp_rw [cgf]
  rw [‚Üê log_prod _ _ fun j hj => ?_]
  ¬∑ rw [h_indep.mgf_sum h_meas]
  ¬∑ exact (mgf_pos (h_int j hj)).ne'

end IndepFun

section Chernoff

/-- **Chernoff bound** on the upper tail of a real random variable. -/
theorem measure_ge_le_exp_mul_mgf [IsFiniteMeasure Œº] (Œµ : ‚Ñù) (ht : 0 ‚â§ t)
    (h_int : Integrable (fun œâ => exp (t * X œâ)) Œº) :
    (Œº {œâ | Œµ ‚â§ X œâ}).toReal ‚â§ exp (-t * Œµ) * mgf X Œº t := by
  rcases ht.eq_or_lt with ht_zero_eq | ht_pos
  ¬∑ rw [ht_zero_eq.symm]
    simp only [neg_zero, zero_mul, exp_zero, mgf_zero', one_mul]
    gcongr
    exacts [measure_ne_top _ _, Set.subset_univ _]
  calc
    (Œº {œâ | Œµ ‚â§ X œâ}).toReal = (Œº {œâ | exp (t * Œµ) ‚â§ exp (t * X œâ)}).toReal := by
      congr with œâ
      simp only [Set.mem_setOf_eq, exp_le_exp, gt_iff_lt]
      exact ‚ü®fun h => mul_le_mul_of_nonneg_left h ht_pos.le,
        fun h => le_of_mul_le_mul_left h ht_pos‚ü©
    _ ‚â§ (exp (t * Œµ))‚Åª¬π * Œº[fun œâ => exp (t * X œâ)] := by
      have : exp (t * Œµ) * (Œº {œâ | exp (t * Œµ) ‚â§ exp (t * X œâ)}).toReal ‚â§
          Œº[fun œâ => exp (t * X œâ)] :=
        mul_meas_ge_le_integral_of_nonneg (ae_of_all _ fun x => (exp_pos _).le) h_int _
      rwa [mul_comm (exp (t * Œµ))‚Åª¬π, ‚Üê div_eq_mul_inv, le_div_iff‚ÇÄ' (exp_pos _)]
    _ = exp (-t * Œµ) * mgf X Œº t := by rw [neg_mul, exp_neg]; rfl

/-- **Chernoff bound** on the lower tail of a real random variable. -/
theorem measure_le_le_exp_mul_mgf [IsFiniteMeasure Œº] (Œµ : ‚Ñù) (ht : t ‚â§ 0)
    (h_int : Integrable (fun œâ => exp (t * X œâ)) Œº) :
    (Œº {œâ | X œâ ‚â§ Œµ}).toReal ‚â§ exp (-t * Œµ) * mgf X Œº t := by
  rw [‚Üê neg_neg t, ‚Üê mgf_neg, neg_neg, ‚Üê neg_mul_neg (-t)]
  refine Eq.trans_le ?_ (measure_ge_le_exp_mul_mgf (-Œµ) (neg_nonneg.mpr ht) ?_)
  ¬∑ congr with œâ
    simp only [Pi.neg_apply, neg_le_neg_iff]
  ¬∑ simp_rw [Pi.neg_apply, neg_mul_neg]
    exact h_int

/-- **Chernoff bound** on the upper tail of a real random variable. -/
theorem measure_ge_le_exp_cgf [IsFiniteMeasure Œº] (Œµ : ‚Ñù) (ht : 0 ‚â§ t)
    (h_int : Integrable (fun œâ => exp (t * X œâ)) Œº) :
    (Œº {œâ | Œµ ‚â§ X œâ}).toReal ‚â§ exp (-t * Œµ + cgf X Œº t) := by
  refine (measure_ge_le_exp_mul_mgf Œµ ht h_int).trans ?_
  rw [exp_add]
  exact mul_le_mul le_rfl (le_exp_log _) mgf_nonneg (exp_pos _).le

/-- **Chernoff bound** on the lower tail of a real random variable. -/
theorem measure_le_le_exp_cgf [IsFiniteMeasure Œº] (Œµ : ‚Ñù) (ht : t ‚â§ 0)
    (h_int : Integrable (fun œâ => exp (t * X œâ)) Œº) :
    (Œº {œâ | X œâ ‚â§ Œµ}).toReal ‚â§ exp (-t * Œµ + cgf X Œº t) := by
  refine (measure_le_le_exp_mul_mgf Œµ ht h_int).trans ?_
  rw [exp_add]
  exact mul_le_mul le_rfl (le_exp_log _) mgf_nonneg (exp_pos _).le

end Chernoff

end MomentGeneratingFunction

end ProbabilityTheory
