/-
Copyright (c) 2022 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Analysis.Calculus.IteratedDeriv.Defs
import Mathlib.Analysis.SpecialFunctions.Exponential
import Mathlib.Data.Real.StarOrdered
import Mathlib.MeasureTheory.Integral.DominatedConvergence
import Mathlib.Probability.Variance

/-!
# Moments and moment generating function

## Main definitions

* `ProbabilityTheory.moment X p Î¼`: `p`th moment of a real random variable `X` with respect to
  measure `Î¼`, `Î¼[X^p]`
* `ProbabilityTheory.centralMoment X p Î¼`:`p`th central moment of `X` with respect to measure `Î¼`,
  `Î¼[(X - Î¼[X])^p]`
* `ProbabilityTheory.mgf X Î¼ t`: moment generating function of `X` with respect to measure `Î¼`,
  `Î¼[exp(t*X)]`
* `ProbabilityTheory.cgf X Î¼ t`: cumulant generating function, logarithm of the moment generating
  function

## Main results

* `ProbabilityTheory.IndepFun.mgf_add`: if two real random variables `X` and `Y` are independent
  and their mgfs are defined at `t`, then `mgf (X + Y) Î¼ t = mgf X Î¼ t * mgf Y Î¼ t`
* `ProbabilityTheory.IndepFun.cgf_add`: if two real random variables `X` and `Y` are independent
  and their cgfs are defined at `t`, then `cgf (X + Y) Î¼ t = cgf X Î¼ t + cgf Y Î¼ t`
* `ProbabilityTheory.measure_ge_le_exp_cgf` and `ProbabilityTheory.measure_le_le_exp_cgf`:
  Chernoff bound on the upper (resp. lower) tail of a random variable. For `t` nonnegative such that
  the cgf exists, `â„™(Îµ â‰¤ X) â‰¤ exp(- t*Îµ + cgf X â„™ t)`. See also
  `ProbabilityTheory.measure_ge_le_exp_mul_mgf` and
  `ProbabilityTheory.measure_le_le_exp_mul_mgf` for versions of these results using `mgf` instead
  of `cgf`.

-/


open MeasureTheory Filter Finset Real

noncomputable section

open scoped MeasureTheory ProbabilityTheory ENNReal NNReal Topology

-- found on zulip
theorem Real.exp_eq_tsum (x : â„) :
    Real.exp x = âˆ‘' n, x^n / n.factorial := by
  rw [Real.exp_eq_exp_â„, NormedSpace.exp_eq_tsum_div]

lemma _root_.Summable.mono {Î² E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
    [FiniteDimensional â„ E] {f g : Î² â†’ E} (hg : Summable g)
    (hfg : âˆ€ b, â€–f bâ€– â‰¤ â€–g bâ€–) :
    Summable f := by
  rw [â† summable_norm_iff] at hg âŠ¢
  refine summable_of_sum_le (c := âˆ‘' x, â€–g xâ€–) (fun _ â†¦ by positivity) (fun s â†¦ ?_)
  exact (sum_le_sum fun i _ â†¦ hfg i).trans (sum_le_tsum s (fun _ _ â†¦ by positivity) hg)

namespace ProbabilityTheory

variable {Î© Î¹ : Type*} {m : MeasurableSpace Î©} {X : Î© â†’ â„} {p : â„•} {Î¼ : Measure Î©}

/-- Moment of a real random variable, `Î¼[X ^ p]`. -/
def moment (X : Î© â†’ â„) (p : â„•) (Î¼ : Measure Î©) : â„ :=
  Î¼[X ^ p]

/-- Central moment of a real random variable, `Î¼[(X - Î¼[X]) ^ p]`. -/
def centralMoment (X : Î© â†’ â„) (p : â„•) (Î¼ : Measure Î©) : â„ := by
  have m := fun (x : Î©) => Î¼[X] -- Porting note: Lean deems `Î¼[(X - fun x => Î¼[X]) ^ p]` ambiguous
  exact Î¼[(X - m) ^ p]

@[simp]
theorem moment_zero (hp : p â‰  0) : moment 0 p Î¼ = 0 := by
  simp only [moment, hp, zero_pow, Ne, not_false_iff, Pi.zero_apply, integral_const,
    smul_eq_mul, mul_zero, integral_zero]

@[simp]
lemma moment_zero_measure : moment X p (0 : Measure Î©) = 0 := by simp [moment]

@[simp]
theorem centralMoment_zero (hp : p â‰  0) : centralMoment 0 p Î¼ = 0 := by
  simp only [centralMoment, hp, Pi.zero_apply, integral_const, smul_eq_mul,
    mul_zero, zero_sub, Pi.pow_apply, Pi.neg_apply, neg_zero, zero_pow, Ne, not_false_iff]

@[simp]
lemma centralMoment_zero_measure : centralMoment X p (0 : Measure Î©) = 0 := by
  simp [centralMoment]

theorem centralMoment_one' [IsFiniteMeasure Î¼] (h_int : Integrable X Î¼) :
    centralMoment X 1 Î¼ = (1 - (Î¼ Set.univ).toReal) * Î¼[X] := by
  simp only [centralMoment, Pi.sub_apply, pow_one]
  rw [integral_sub h_int (integrable_const _)]
  simp only [sub_mul, integral_const, smul_eq_mul, one_mul]

@[simp]
theorem centralMoment_one [IsZeroOrProbabilityMeasure Î¼] : centralMoment X 1 Î¼ = 0 := by
  rcases eq_zero_or_isProbabilityMeasure Î¼ with rfl | h
  Â· simp [centralMoment]
  by_cases h_int : Integrable X Î¼
  Â· rw [centralMoment_one' h_int]
    simp only [measure_univ, ENNReal.one_toReal, sub_self, zero_mul]
  Â· simp only [centralMoment, Pi.sub_apply, pow_one]
    have : Â¬Integrable (fun x => X x - integral Î¼ X) Î¼ := by
      refine fun h_sub => h_int ?_
      have h_add : X = (fun x => X x - integral Î¼ X) + fun _ => integral Î¼ X := by ext1 x; simp
      rw [h_add]
      exact h_sub.add (integrable_const _)
    rw [integral_undef this]

theorem centralMoment_two_eq_variance [IsFiniteMeasure Î¼] (hX : Memâ„’p X 2 Î¼) :
    centralMoment X 2 Î¼ = variance X Î¼ := by rw [hX.variance_eq]; rfl

section MomentGeneratingFunction

variable {t u v : â„}

@[simp]
lemma FormalMultilinearSeries.coeff_ofScalars {p : â„• â†’ â„} {n : â„•} :
    (FormalMultilinearSeries.ofScalars â„ p).coeff n = p n := by
  simp [FormalMultilinearSeries.coeff, FormalMultilinearSeries.ofScalars, List.prod_ofFn]

lemma _root_.AEMeasurable.abs (hX : AEMeasurable X Î¼) : AEMeasurable (fun Ï‰ â†¦ |X Ï‰|) Î¼ :=
  hX.max (aemeasurable_neg_iff.mpr hX)

lemma aemeasurable_of_aemeasurable_exp_mul (ht : t â‰  0)
    (hX : AEMeasurable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼) :
    AEMeasurable X Î¼ := by
  suffices AEMeasurable (fun Ï‰ â†¦ t * X Ï‰) Î¼ by
    have h_eq : X = fun Ï‰ â†¦ (t * X Ï‰) / t := by ext Ï‰; field_simp
    rw [h_eq]
    exact this.div aemeasurable_const
  exact aemeasurable_of_aemeasurable_exp hX

section Integrable

/-- Auxiliary lemma for `integrable_exp_mul_of_le`. -/
lemma integrable_exp_mul_of_le_of_measurable [IsFiniteMeasure Î¼] (hX : Measurable X)
    (hu : Integrable (fun Ï‰ â†¦ exp (u * X Ï‰)) Î¼) (h_nonneg : 0 â‰¤ t) (htu : t â‰¤ u) :
    Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼ := by
  by_cases ht : t = 0
  Â· simp [ht]
  have h_pos : 0 < t := lt_of_le_of_ne' h_nonneg ht
  have hu' : Integrable (1 + {w | 0 â‰¤ X w}.indicator (fun Ï‰ â†¦ exp (u * X Ï‰))) Î¼ :=
    (integrable_const _).add (hu.indicator (hX measurableSet_Ici))
  refine hu'.mono ?_ (ae_of_all _ fun Ï‰ â†¦ ?_)
  Â· have hX : AEMeasurable X Î¼ := aemeasurable_of_aemeasurable_exp_mul (h_pos.trans_le htu).ne'
      hu.aemeasurable
    exact (measurable_exp.comp_aemeasurable (hX.const_mul _)).aestronglyMeasurable
  Â· simp only [norm_eq_abs, abs_exp, Pi.add_apply, Pi.one_apply]
    rw [abs_of_nonneg]
    swap; Â· exact add_nonneg zero_le_one (Set.indicator_nonneg (fun Ï‰ _ â†¦ by positivity) _)
    rcases le_or_lt 0 (X Ï‰) with h_nonneg | h_neg
    Â· simp only [Set.mem_setOf_eq, h_nonneg, Set.indicator_of_mem]
      calc rexp (t * X Ï‰) â‰¤ 1 + rexp (t * X Ï‰) := (le_add_iff_nonneg_left _).mpr zero_le_one
      _ â‰¤ 1 + exp (u * X Ï‰) := by gcongr
    Â· simp only [Set.mem_setOf_eq, not_le.mpr h_neg, not_false_eq_true, Set.indicator_of_not_mem,
        add_zero, exp_le_one_iff]
      exact mul_nonpos_of_nonneg_of_nonpos h_pos.le h_neg.le

/-- If `Ï‰ â†¦ exp (u * X Ï‰)` is integrable at `u â‰¥ 0`, then it is integrable on `[0, u]`. -/
lemma integrable_exp_mul_of_le [IsFiniteMeasure Î¼]
    (hu : Integrable (fun Ï‰ â†¦ exp (u * X Ï‰)) Î¼) (h_nonneg : 0 â‰¤ t) (htu : t â‰¤ u) :
    Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼ := by
  by_cases ht : t = 0
  Â· simp [ht]
  have hX : AEMeasurable X Î¼ := by
    refine aemeasurable_of_aemeasurable_exp_mul ?_ hu.aemeasurable
    exact ((lt_of_le_of_ne' h_nonneg ht).trans_le htu).ne'
  have h_eq t : (fun Ï‰ â†¦ exp (t * X Ï‰)) =áµ[Î¼] fun Ï‰ â†¦ exp (t * hX.mk X Ï‰) := by
    filter_upwards [hX.ae_eq_mk] with Ï‰ hÏ‰ using by rw [hÏ‰]
  rw [integrable_congr (h_eq t)]
  rw [integrable_congr (h_eq u)] at hu
  exact integrable_exp_mul_of_le_of_measurable hX.measurable_mk hu h_nonneg htu

/-- If `Ï‰ â†¦ exp (u * X Ï‰)` is integrable at `u â‰¤ 0`, then it is integrable on `[u, 0]`. -/
lemma integrable_exp_mul_of_ge [IsFiniteMeasure Î¼]
    (hu : Integrable (fun Ï‰ â†¦ exp (u * X Ï‰)) Î¼) (h_nonpos : t â‰¤ 0) (htu : u â‰¤ t) :
    Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼ := by
  suffices Integrable (fun Ï‰ â†¦ exp (- t * (-X) Ï‰)) Î¼ by simpa using this
  exact integrable_exp_mul_of_le (u := -u) (t := -t)
    (by simpa using hu) (by simp [h_nonpos]) (by simp [htu])

/-- If `Ï‰ â†¦ exp (u * X Ï‰)` is integrable at `u` and `-u`, then it is integrable on `[-u, u]`. -/
lemma integrable_exp_mul_of_abs_le [IsFiniteMeasure Î¼]
    (hu_int_pos : Integrable (fun Ï‰ â†¦ exp (u * X Ï‰)) Î¼)
    (hu_int_neg : Integrable (fun Ï‰ â†¦ exp (- u * X Ï‰)) Î¼)
    (htu : |t| â‰¤ |u|) :
    Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼ := by
  rcases le_total 0 t with ht | ht
  Â· rw [abs_of_nonneg ht] at htu
    refine integrable_exp_mul_of_le ?_ ht htu
    rcases le_total 0 u with hu | hu
    Â· rwa [abs_of_nonneg hu]
    Â· rwa [abs_of_nonpos hu]
  Â· rw [abs_of_nonpos ht, neg_le] at htu
    refine integrable_exp_mul_of_ge ?_ ht htu
    rcases le_total 0 u with hu | hu
    Â· rwa [abs_of_nonneg hu]
    Â· rwa [abs_of_nonpos hu, neg_neg]

lemma integrable_exp_mul_of_le_of_le [IsFiniteMeasure Î¼] {a b : â„}
    (ha : Integrable (fun Ï‰ â†¦ exp (a * X Ï‰)) Î¼)
    (hb : Integrable (fun Ï‰ â†¦ exp (b * X Ï‰)) Î¼)
    (hat : a â‰¤ t) (htb : t â‰¤ b) :
    Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼ := by
  rcases le_total 0 t with ht | ht
  Â· exact integrable_exp_mul_of_le hb ht htb
  Â· exact integrable_exp_mul_of_ge ha ht hat

lemma exp_mul_abs_add_le_add : exp (t * |u| + v * u) â‰¤ rexp ((v + t) * u) + rexp ((v - t) * u) := by
  rcases le_total 0 u with h_nonneg | h_nonpos
  Â· rw [abs_of_nonneg h_nonneg, â† add_mul, add_comm, le_add_iff_nonneg_right]
    positivity
  Â· rw [abs_of_nonpos h_nonpos, mul_neg, mul_comm, â† mul_neg, mul_comm, â† add_mul, add_comm,
      â† sub_eq_add_neg, le_add_iff_nonneg_left]
    positivity

lemma exp_mul_abs_le_add : exp (t * |u|) â‰¤ rexp (t * u) + rexp (-(t * u)) := by
  have h := exp_mul_abs_add_le_add (t := t) (u := u) (v := 0)
  simpa using h

lemma integrable_exp_mul_abs_add (ht_int_pos : Integrable (fun Ï‰ â†¦ rexp ((v + t) * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ rexp ((v - t) * X Ï‰)) Î¼) :
    Integrable (fun Ï‰ â†¦ rexp (t * |X Ï‰| + v * X Ï‰)) Î¼ := by
  have h_int_add : Integrable (fun a â†¦ rexp ((v + t) * X a) + rexp ((v - t) * X a)) Î¼ :=
    ht_int_pos.add <| by simpa using ht_int_neg
  refine Integrable.mono h_int_add ?_ (ae_of_all _ fun Ï‰ â†¦ ?_)
  Â· by_cases ht : t = 0
    Â· simp only [ht, zero_mul, zero_add]
      simp only [ht, add_zero] at ht_int_pos
      exact ht_int_pos.1
    have hX : AEMeasurable X Î¼ := by
      by_cases hvt : v + t = 0
      Â· have hvt' : v - t â‰  0 := by
          rw [sub_ne_zero]
          refine fun h_eq â†¦ ht ?_
          simpa [h_eq] using hvt
        exact aemeasurable_of_aemeasurable_exp_mul hvt' ht_int_neg.aemeasurable
      Â· exact aemeasurable_of_aemeasurable_exp_mul hvt ht_int_pos.aemeasurable
    refine AEMeasurable.aestronglyMeasurable ?_
    exact measurable_exp.comp_aemeasurable ((hX.abs.const_mul _).add (hX.const_mul _))
  Â· simp only [norm_eq_abs, abs_exp]
    conv_rhs => rw [abs_of_nonneg (by positivity)]
    exact exp_mul_abs_add_le_add

/-- If `Ï‰ â†¦ rexp (t * X Ï‰)` is integrable at `t` and `-t`, then `Ï‰ â†¦ rexp (t * |X Ï‰|)` is
integrable. -/
lemma integrable_exp_mul_abs (ht_int_pos : Integrable (fun Ï‰ â†¦ rexp (t * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ rexp (- t * X Ï‰)) Î¼) :
    Integrable (fun Ï‰ â†¦ rexp (t * |X Ï‰|)) Î¼ := by
  have h := integrable_exp_mul_abs_add (t := t) (Î¼ := Î¼) (X := X) (v := 0) ?_ ?_
  Â· simpa using h
  Â· simpa using ht_int_pos
  Â· simpa using ht_int_neg

lemma integrable_exp_abs_mul_abs_add (ht_int_pos : Integrable (fun Ï‰ â†¦ rexp ((v + t) * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ rexp ((v - t) * X Ï‰)) Î¼) :
    Integrable (fun Ï‰ â†¦ rexp (|t| * |X Ï‰| + v * X Ï‰)) Î¼ := by
  rcases le_total 0 t with ht_nonneg | ht_nonpos
  Â· simp_rw [abs_of_nonneg ht_nonneg]
    exact integrable_exp_mul_abs_add ht_int_pos ht_int_neg
  Â· simp_rw [abs_of_nonpos ht_nonpos]
    exact integrable_exp_mul_abs_add ht_int_neg (by simpa using ht_int_pos)

/-- If `Ï‰ â†¦ rexp (t * X Ï‰)` is integrable at `t` and `-t`, then `Ï‰ â†¦ rexp (|t| * |X Ï‰|)` is
integrable. -/
lemma integrable_exp_abs_mul_abs (ht_int_pos : Integrable (fun Ï‰ â†¦ rexp (t * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ rexp (- t * X Ï‰)) Î¼) :
    Integrable (fun Ï‰ â†¦ rexp (|t| * |X Ï‰|)) Î¼ := by
  rcases le_total 0 t with ht_nonneg | ht_nonpos
  Â· simp_rw [abs_of_nonneg ht_nonneg]
    exact integrable_exp_mul_abs ht_int_pos ht_int_neg
  Â· simp_rw [abs_of_nonpos ht_nonpos]
    exact integrable_exp_mul_abs ht_int_neg (by simpa using ht_int_pos)

lemma integrable_pow_abs_mul_exp_of_integrable_exp_mul (ht : t â‰  0)
    (ht_int_pos : Integrable (fun x â†¦ rexp ((v + t) * X x)) Î¼)
    (ht_int_neg : Integrable (fun x â†¦ rexp ((v - t) * X x)) Î¼) (n : â„•) :
    Integrable (fun Ï‰ â†¦ |X Ï‰| ^ n * exp (v * X Ï‰)) Î¼ := by
  suffices Integrable (fun Ï‰ â†¦ (t * |X Ï‰|) ^ n / n.factorial * exp (v * X Ï‰)) Î¼ by
    have h_eq Ï‰ : |X Ï‰| ^ n * exp (v * X Ï‰)
        = ((t * |X Ï‰|) ^ n / n.factorial * exp (v * X Ï‰)) * n.factorial / t ^ n := by
      rw [mul_pow]
      field_simp
      ring
    simp_rw [h_eq]
    exact (this.mul_const _).div_const _
  have h_le Ï‰ : (|t| * |X Ï‰|) ^ n / n.factorial â‰¤ exp (|t| * |X Ï‰|) :=
    pow_div_factorial_le_exp _ (by positivity) _
  have h_int := integrable_exp_abs_mul_abs_add ht_int_pos ht_int_neg
  refine Integrable.mono h_int ?_ (ae_of_all _ fun Ï‰ â†¦ ?_)
  Â· have hX : AEMeasurable X Î¼ := by
      by_cases hvt : v + t = 0
      Â· have hvt' : v - t â‰  0 := by
          rw [sub_ne_zero]
          refine fun h_eq â†¦ ht ?_
          simpa [h_eq] using hvt
        exact aemeasurable_of_aemeasurable_exp_mul hvt' ht_int_neg.aemeasurable
      Â· exact aemeasurable_of_aemeasurable_exp_mul hvt ht_int_pos.aemeasurable
    simp_rw [mul_pow]
    refine AEMeasurable.aestronglyMeasurable ?_
    exact (((hX.abs.pow_const _).const_mul _).div_const _).mul
      (measurable_exp.comp_aemeasurable (hX.const_mul _))
  Â· simp only [norm_div, norm_pow, norm_mul, norm_eq_abs, abs_abs, norm_natCast, abs_exp,
      Nat.abs_cast]
    rw [exp_add]
    gcongr
    exact h_le _

lemma add_half_inf_sub_mem_Ioo {l u v : â„} (hv : v âˆˆ Set.Ioo l u) :
    v + ((v - l) âŠ“ (u - v)) / 2 âˆˆ Set.Ioo l u := by
  have h_pos : 0 < (v - l) âŠ“ (u - v) := by simp [hv.1, hv.2]
  constructor
  Â· calc l < v := hv.1
    _ â‰¤ v + ((v - l) âŠ“ (u - v)) / 2 := le_add_of_nonneg_right (by positivity)
  Â· calc v + ((v - l) âŠ“ (u - v)) / 2
    _ < v + ((v - l) âŠ“ (u - v)) := by gcongr; exact half_lt_self (by positivity)
    _ â‰¤ v + (u - v) := by gcongr; exact inf_le_right
    _ = u := by abel

lemma sub_half_inf_sub_mem_Ioo {l u v : â„} (hv : v âˆˆ Set.Ioo l u) :
    v - ((v - l) âŠ“ (u - v)) / 2 âˆˆ Set.Ioo l u := by
  have h_pos : 0 < (v - l) âŠ“ (u - v) := by simp [hv.1, hv.2]
  constructor
  Â· calc l = v - (v - l) := by abel
    _ â‰¤ v - ((v - l) âŠ“ (u - v)) := by gcongr; exact inf_le_left
    _ < v - ((v - l) âŠ“ (u - v)) / 2 := by gcongr; exact half_lt_self (by positivity)
  Â· calc v - ((v - l) âŠ“ (u - v)) / 2
    _ â‰¤ v := by
      rw [sub_le_iff_le_add]
      exact le_add_of_nonneg_right (by positivity)
    _ < u := hv.2

lemma integrable_pow_abs_mul_exp_of_mem_interior
    (hv : v âˆˆ interior {t | Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼}) (n : â„•) :
    Integrable (fun Ï‰ â†¦ |X Ï‰| ^ n * exp (v * X Ï‰)) Î¼ := by
  rw [mem_interior_iff_mem_nhds, mem_nhds_iff_exists_Ioo_subset] at hv
  obtain âŸ¨l, u, hvlu, h_subsetâŸ© := hv
  have h_pos : 0 < (v - l) âŠ“ (u - v) := by simp [hvlu.1, hvlu.2]
  refine integrable_pow_abs_mul_exp_of_integrable_exp_mul (t := min (v - l) (u - v) / 2) ?_ ?_ ?_ n
  Â· positivity
  Â· exact h_subset (add_half_inf_sub_mem_Ioo hvlu)
  Â· exact h_subset (sub_half_inf_sub_mem_Ioo hvlu)

/-- If `Ï‰ â†¦ rexp (t * X Ï‰)` is integrable at `t` and `-t` for `t â‰  0`, then `Ï‰ â†¦ |X Ï‰| ^ n` is
integrable for all `n : â„•`. That is, all moments of `X` are finite. -/
lemma integrable_pow_abs_of_integrable_exp_mul (ht : t â‰  0)
    (ht_int_pos : Integrable (fun x â†¦ rexp (t * X x)) Î¼)
    (ht_int_neg : Integrable (fun x â†¦ rexp (- t * X x)) Î¼) (n : â„•) :
    Integrable (fun Ï‰ â†¦ |X Ï‰| ^ n) Î¼ := by
  have h := integrable_pow_abs_mul_exp_of_integrable_exp_mul (Î¼ := Î¼) (X := X) ht (v := 0) ?_ ?_ n
  Â· simpa using h
  Â· simpa using ht_int_pos
  Â· simpa using ht_int_neg

lemma integrable_pow_mul_exp_of_integrable_exp_mul (ht : t â‰  0)
    (ht_int_pos : Integrable (fun x â†¦ rexp ((v + t) * X x)) Î¼)
    (ht_int_neg : Integrable (fun x â†¦ rexp ((v - t) * X x)) Î¼) (n : â„•) :
    Integrable (fun Ï‰ â†¦ X Ï‰ ^ n * exp (v * X Ï‰)) Î¼ := by
  rw [â† integrable_norm_iff]
  Â· simp_rw [norm_eq_abs, abs_mul, abs_pow, abs_exp]
    exact integrable_pow_abs_mul_exp_of_integrable_exp_mul ht ht_int_pos ht_int_neg n
  Â· have hX : AEMeasurable X Î¼ := by
      by_cases hvt : v + t = 0
      Â· have hvt' : v - t â‰  0 := by
          rw [sub_ne_zero]
          refine fun h_eq â†¦ ht ?_
          simpa [h_eq] using hvt
        exact aemeasurable_of_aemeasurable_exp_mul hvt' ht_int_neg.aemeasurable
      Â· exact aemeasurable_of_aemeasurable_exp_mul hvt ht_int_pos.aemeasurable
    exact ((hX.pow_const _).mul
      (measurable_exp.comp_aemeasurable (hX.const_mul _))).aestronglyMeasurable

lemma aemeasurable_of_mem_interior_integrable_exp
    (hv : v âˆˆ interior {t | Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼}) :
    AEMeasurable X Î¼ := by
  rw [mem_interior_iff_mem_nhds, mem_nhds_iff_exists_Ioo_subset] at hv
  obtain âŸ¨l, u, hvlu, h_subsetâŸ© := hv
  let t := ((v - l) âŠ“ (u - v)) / 2
  have h_pos : 0 < (v - l) âŠ“ (u - v) := by simp [hvlu.1, hvlu.2]
  have ht : 0 < t := half_pos h_pos
  by_cases hvt : v + t = 0
  Â· have hvt' : v - t â‰  0 := by
      rw [sub_ne_zero]
      refine fun h_eq â†¦ ht.ne' ?_
      simpa [h_eq] using hvt
    exact aemeasurable_of_aemeasurable_exp_mul hvt'
      (h_subset (sub_half_inf_sub_mem_Ioo hvlu)).aemeasurable
  Â· exact aemeasurable_of_aemeasurable_exp_mul hvt
      (h_subset (add_half_inf_sub_mem_Ioo hvlu)).aemeasurable

lemma integrable_pow_mul_exp_of_mem_interior
    (hv : v âˆˆ interior {t | Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼}) (n : â„•) :
    Integrable (fun Ï‰ â†¦ X Ï‰ ^ n * exp (v * X Ï‰)) Î¼ := by
  rw [â† integrable_norm_iff]
  Â· simp_rw [norm_eq_abs, abs_mul, abs_pow, abs_exp]
    exact integrable_pow_abs_mul_exp_of_mem_interior hv n
  Â· have hX : AEMeasurable X Î¼ := aemeasurable_of_mem_interior_integrable_exp hv
    exact ((hX.pow_const _).mul
      (measurable_exp.comp_aemeasurable (hX.const_mul _))).aestronglyMeasurable

/-- If `Ï‰ â†¦ rexp (t * X Ï‰)` is integrable at `t` and `-t` for `t â‰  0`, then `Ï‰ â†¦ X Ï‰ ^ n` is
integrable for all `n : â„•`. -/
lemma integrable_pow_of_integrable_exp_mul (ht : t â‰  0)
    (ht_int_pos : Integrable (fun x â†¦ rexp (t * X x)) Î¼)
    (ht_int_neg : Integrable (fun x â†¦ rexp (- t * X x)) Î¼) (n : â„•) :
    Integrable (fun Ï‰ â†¦ X Ï‰ ^ n) Î¼ := by
  have h := integrable_pow_mul_exp_of_integrable_exp_mul (Î¼ := Î¼) (X := X) ht (v := 0) ?_ ?_ n
  Â· simpa using h
  Â· simpa using ht_int_pos
  Â· simpa using ht_int_neg

end Integrable

/-- Moment generating function of a real random variable `X`: `fun t => Î¼[exp(t*X)]`. -/
def mgf (X : Î© â†’ â„) (Î¼ : Measure Î©) (t : â„) : â„ :=
  Î¼[fun Ï‰ => exp (t * X Ï‰)]

/-- Cumulant generating function of a real random variable `X`: `fun t => log Î¼[exp(t*X)]`. -/
def cgf (X : Î© â†’ â„) (Î¼ : Measure Î©) (t : â„) : â„ :=
  log (mgf X Î¼ t)

@[simp]
theorem mgf_zero_fun : mgf 0 Î¼ t = (Î¼ Set.univ).toReal := by
  simp only [mgf, Pi.zero_apply, mul_zero, exp_zero, integral_const, smul_eq_mul, mul_one]

@[simp]
theorem cgf_zero_fun : cgf 0 Î¼ t = log (Î¼ Set.univ).toReal := by simp only [cgf, mgf_zero_fun]

@[simp]
theorem mgf_zero_measure : mgf X (0 : Measure Î©) t = 0 := by simp only [mgf, integral_zero_measure]

@[simp]
theorem cgf_zero_measure : cgf X (0 : Measure Î©) t = 0 := by
  simp only [cgf, log_zero, mgf_zero_measure]

@[simp]
theorem mgf_const' (c : â„) : mgf (fun _ => c) Î¼ t = (Î¼ Set.univ).toReal * exp (t * c) := by
  simp only [mgf, integral_const, smul_eq_mul]

-- @[simp] -- Porting note: `simp only` already proves this
theorem mgf_const (c : â„) [IsProbabilityMeasure Î¼] : mgf (fun _ => c) Î¼ t = exp (t * c) := by
  simp only [mgf_const', measure_univ, ENNReal.one_toReal, one_mul]

@[simp]
theorem cgf_const' [IsFiniteMeasure Î¼] (hÎ¼ : Î¼ â‰  0) (c : â„) :
    cgf (fun _ => c) Î¼ t = log (Î¼ Set.univ).toReal + t * c := by
  simp only [cgf, mgf_const']
  rw [log_mul _ (exp_pos _).ne']
  Â· rw [log_exp _]
  Â· rw [Ne, ENNReal.toReal_eq_zero_iff, Measure.measure_univ_eq_zero]
    simp only [hÎ¼, measure_ne_top Î¼ Set.univ, or_self_iff, not_false_iff]

@[simp]
theorem cgf_const [IsProbabilityMeasure Î¼] (c : â„) : cgf (fun _ => c) Î¼ t = t * c := by
  simp only [cgf, mgf_const, log_exp]

@[simp]
theorem mgf_zero' : mgf X Î¼ 0 = (Î¼ Set.univ).toReal := by
  simp only [mgf, zero_mul, exp_zero, integral_const, smul_eq_mul, mul_one]

-- @[simp] -- Porting note: `simp only` already proves this
theorem mgf_zero [IsProbabilityMeasure Î¼] : mgf X Î¼ 0 = 1 := by
  simp only [mgf_zero', measure_univ, ENNReal.one_toReal]

theorem cgf_zero' : cgf X Î¼ 0 = log (Î¼ Set.univ).toReal := by simp only [cgf, mgf_zero']

@[simp]
theorem cgf_zero [IsZeroOrProbabilityMeasure Î¼] : cgf X Î¼ 0 = 0 := by
  rcases eq_zero_or_isProbabilityMeasure Î¼ with rfl | h <;> simp [cgf_zero']

theorem mgf_undef (hX : Â¬Integrable (fun Ï‰ => exp (t * X Ï‰)) Î¼) : mgf X Î¼ t = 0 := by
  simp only [mgf, integral_undef hX]

theorem cgf_undef (hX : Â¬Integrable (fun Ï‰ => exp (t * X Ï‰)) Î¼) : cgf X Î¼ t = 0 := by
  simp only [cgf, mgf_undef hX, log_zero]

theorem mgf_nonneg : 0 â‰¤ mgf X Î¼ t := by
  unfold mgf; positivity

theorem mgf_pos' (hÎ¼ : Î¼ â‰  0) (h_int_X : Integrable (fun Ï‰ => exp (t * X Ï‰)) Î¼) :
    0 < mgf X Î¼ t := by
  simp_rw [mgf]
  have : âˆ« x : Î©, exp (t * X x) âˆ‚Î¼ = âˆ« x : Î© in Set.univ, exp (t * X x) âˆ‚Î¼ := by
    simp only [Measure.restrict_univ]
  rw [this, setIntegral_pos_iff_support_of_nonneg_ae _ _]
  Â· have h_eq_univ : (Function.support fun x : Î© => exp (t * X x)) = Set.univ := by
      ext1 x
      simp only [Function.mem_support, Set.mem_univ, iff_true]
      exact (exp_pos _).ne'
    rw [h_eq_univ, Set.inter_univ _]
    refine Ne.bot_lt ?_
    simp only [hÎ¼, ENNReal.bot_eq_zero, Ne, Measure.measure_univ_eq_zero, not_false_iff]
  Â· filter_upwards with x
    rw [Pi.zero_apply]
    exact (exp_pos _).le
  Â· rwa [integrableOn_univ]

theorem mgf_pos [IsProbabilityMeasure Î¼] (h_int_X : Integrable (fun Ï‰ => exp (t * X Ï‰)) Î¼) :
    0 < mgf X Î¼ t :=
  mgf_pos' (IsProbabilityMeasure.ne_zero Î¼) h_int_X

/-- `exp cgf = mgf`.
For a version for probability measures, without the hypothesis `hÎ¼ : Î¼ â‰  0`, see `exp_cgf`. -/
lemma exp_cgf' (hÎ¼ : Î¼ â‰  0) (h_int_X : Integrable (fun Ï‰ => exp (t * X Ï‰)) Î¼) :
    exp (cgf X Î¼ t) = mgf X Î¼ t := by rw [cgf, exp_log (mgf_pos' hÎ¼ h_int_X)]

/-- `exp cgf = mgf`.
For a version that works more generally than probability measures, with a hypothesis `hÎ¼ : Î¼ â‰  0`,
see `exp_cgf'`. -/
lemma exp_cgf [IsProbabilityMeasure Î¼] (h_int_X : Integrable (fun Ï‰ => exp (t * X Ï‰)) Î¼) :
    exp (cgf X Î¼ t) = mgf X Î¼ t := by rw [cgf, exp_log (mgf_pos h_int_X)]

theorem mgf_neg : mgf (-X) Î¼ t = mgf X Î¼ (-t) := by simp_rw [mgf, Pi.neg_apply, mul_neg, neg_mul]

theorem cgf_neg : cgf (-X) Î¼ t = cgf X Î¼ (-t) := by simp_rw [cgf, mgf_neg]

theorem mgf_smul_left (Î± : â„) : mgf (Î± â€¢ X) Î¼ t = mgf X Î¼ (Î± * t) := by
  simp_rw [mgf, Pi.smul_apply, smul_eq_mul, mul_comm Î± t, mul_assoc]

/-- The moment generating function is monotone in the random variable for `t â‰¥ 0`. -/
lemma mgf_mono_of_nonneg {Y : Î© â†’ â„} (hXY : X â‰¤áµ[Î¼] Y) (ht : 0 â‰¤ t)
    (htY : Integrable (fun Ï‰ â†¦ exp (t * Y Ï‰)) Î¼) :
    mgf X Î¼ t â‰¤ mgf Y Î¼ t := by
  by_cases htX : Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼
  Â· refine integral_mono_ae htX htY ?_
    filter_upwards [hXY] with Ï‰ hÏ‰ using by gcongr
  Â· rw [mgf_undef htX]
    exact mgf_nonneg

/-- The moment generating function is antitone in the random variable for `t â‰¤ 0`. -/
lemma mgf_anti_of_nonpos {Y : Î© â†’ â„} (hXY : X â‰¤áµ[Î¼] Y) (ht : t â‰¤ 0)
    (htX : Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼) :
    mgf Y Î¼ t â‰¤ mgf X Î¼ t := by
  by_cases htY : Integrable (fun Ï‰ â†¦ exp (t * Y Ï‰)) Î¼
  Â· refine integral_mono_ae htY htX ?_
    filter_upwards [hXY] with Ï‰ hÏ‰ using exp_monotone <| mul_le_mul_of_nonpos_left hÏ‰ ht
  Â· rw [mgf_undef htY]
    exact mgf_nonneg

lemma mgf_abs_le_add (ht_int_pos : Integrable (fun Ï‰ â†¦ rexp (t * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ rexp (- t * X Ï‰)) Î¼) :
    mgf (fun Ï‰ â†¦ |X Ï‰|) Î¼ t â‰¤ mgf X Î¼ t + mgf (-X) Î¼ t := by
  simp_rw [mgf]
  rw [â† integral_add ht_int_pos (by simpa using ht_int_neg)]
  have h_int_add : Integrable (fun a â†¦ rexp (t * X a) + rexp (-(t * X a))) Î¼ :=
    ht_int_pos.add <| by simpa using ht_int_neg
  simp only [Pi.neg_apply, mul_neg, ge_iff_le]
  refine integral_mono_ae ?_ h_int_add
    (ae_of_all _ (fun Ï‰ â†¦ exp_mul_abs_le_add (t := t) (u := X Ï‰)))
  exact integrable_exp_mul_abs ht_int_pos ht_int_neg

section Analytic

lemma summable_integral_abs_mul_exp
    (ht_int_pos : Integrable (fun Ï‰ â†¦ rexp ((v + t) * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ rexp ((v - t) * X Ï‰)) Î¼) :
    Summable fun (i : â„•) â†¦ Î¼[fun Ï‰ â†¦ |X Ï‰| ^ i / i.factorial * |t| ^ i * exp (v * X Ï‰)] := by
  by_cases ht : t = 0
  Â· simp only [ht, abs_zero]
    refine summable_of_ne_finset_zero (s := {0}) (fun n hn â†¦ ?_)
    rw [zero_pow]
    Â· simp
    Â· simpa using hn
  suffices Summable fun i â†¦ âˆ« Ï‰, (|t| * |X Ï‰|) ^ i / i.factorial * exp (v * X Ï‰) âˆ‚Î¼ by
    simp_rw [mul_pow] at this
    convert this using 4 with i Ï‰
    ring
  have h_int (u : â„) (i : â„•) :
      Integrable (fun Ï‰ â†¦ (u * |X Ï‰|) ^ i / i.factorial * exp (v * X Ï‰)) Î¼ := by
    simp_rw [mul_pow]
    simp_rw [mul_comm _ (exp (v * _)), mul_div]
    refine Integrable.div_const ?_ _
    simp_rw [mul_comm (exp _), mul_assoc]
    refine Integrable.const_mul ?_ _
    exact integrable_pow_abs_mul_exp_of_integrable_exp_mul ht ht_int_pos ht_int_neg i
  refine summable_of_sum_range_le (c := Î¼[fun Ï‰ â†¦ exp (|t| * |X Ï‰| + v * X Ï‰)])
    (fun _ â†¦ integral_nonneg fun Ï‰ â†¦ by positivity) fun n â†¦ ?_
  rw [â† integral_finset_sum]
  Â· refine integral_mono ?_ ?_ ?_
    Â· exact integrable_finset_sum (range n) fun i a â†¦ h_int |t| i
    Â· exact integrable_exp_abs_mul_abs_add ht_int_pos ht_int_neg
    Â· intro Ï‰
      simp only
      rw [â† sum_mul, exp_add]
      gcongr
      exact sum_le_exp_of_nonneg (by positivity) _
  Â· exact fun i _ â†¦ h_int _ i

lemma summable_integral_pow_abs_mul_exp_mul_abs
    (ht_int_pos : Integrable (fun Ï‰ â†¦ rexp ((v + t) * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ rexp ((v - t) * X Ï‰)) Î¼) :
    Summable fun (i : â„•) â†¦ Î¼[fun Ï‰ â†¦ |X Ï‰| ^ i * exp (v * X Ï‰)] / i.factorial * |t| ^ i := by
  simp_rw [â† integral_div, â† integral_mul_right]
  have h_eq i Ï‰ : |X Ï‰| ^ i * rexp (v * X Ï‰) / i.factorial * |t| ^ i
      = |X Ï‰| ^ i / i.factorial * |t| ^ i * rexp (v * X Ï‰) := by ring
  simp_rw [h_eq]
  exact summable_integral_abs_mul_exp ht_int_pos ht_int_neg

lemma summable_integral_pow_mul_exp_mul
    (ht_int_pos : Integrable (fun Ï‰ â†¦ rexp ((v + t) * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ rexp ((v - t) * X Ï‰)) Î¼) :
    Summable fun (i : â„•) â†¦ Î¼[fun Ï‰ â†¦ X Ï‰ ^ i * rexp (v * X Ï‰)] / i.factorial * t ^ i := by
  refine (summable_integral_pow_abs_mul_exp_mul_abs ht_int_pos ht_int_neg).mono fun i â†¦ ?_
  simp only [Pi.pow_apply, norm_mul, norm_div, norm_eq_abs, norm_natCast, norm_pow, abs_abs,
    Nat.abs_cast]
  refine mul_le_mul ?_ le_rfl (by positivity) (by positivity)
  rw [div_le_div_iff_of_pos_right (by positivity)]
  conv_rhs => rw [abs_of_nonneg (integral_nonneg (fun _ â†¦ by positivity))]
  simp_rw [â† norm_eq_abs]
  refine (norm_integral_le_integral_norm _).trans ?_
  simp

lemma summable_integral_pow_mul (ht_int_pos : Integrable (fun Ï‰ â†¦ rexp (t * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ rexp (- t * X Ï‰)) Î¼) :
    Summable fun (i : â„•) â†¦ Î¼[X ^ i] / i.factorial * t ^ i := by
  have h := summable_integral_pow_mul_exp_mul (Î¼ := Î¼) (X := X) (v := 0) (t := t) ?_ ?_
  Â· simpa using h
  Â· simpa using ht_int_pos
  Â· simpa using ht_int_neg

lemma mgf_add_eq_tsum (ht_int_pos : Integrable (fun Ï‰ â†¦ rexp ((v + t) * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ rexp ((v - t) * X Ï‰)) Î¼) :
    mgf X Î¼ (v + t) = âˆ‘' n, Î¼[fun Ï‰ â†¦ X Ï‰ ^ n * exp (v * X Ï‰)] / n.factorial * t ^ n := by
  by_cases ht : t = 0
  Â· rw [tsum_eq_single 0]
    Â· simp [ht, mgf]
    Â· intro n hn
      simp [zero_pow hn, ht]
  have h_int_pow i : Integrable (fun Ï‰ â†¦ X Ï‰ ^ i / i.factorial * t ^ i * exp (v * X Ï‰)) Î¼ := by
    simp_rw [mul_comm _ (exp _), â† mul_assoc, â† mul_div_assoc, mul_comm (exp _)]
    refine (Integrable.div_const ?_ _).mul_const _
    exact integrable_pow_mul_exp_of_integrable_exp_mul ht ht_int_pos ht_int_neg i
  suffices Tendsto (fun n â†¦ |mgf X Î¼ (v + t)
        - Î¼[fun Ï‰ â†¦ âˆ‘ i in range n, X Ï‰ ^ i / i.factorial * t ^ i * exp (v * X Ï‰)]|)
      atTop (ğ“ 0) by
    change Tendsto (abs âˆ˜ _) _ _ at this
    rw [â† tendsto_zero_iff_abs_tendsto_zero] at this
    have h_eq n : Î¼[fun Ï‰ â†¦ âˆ‘ i âˆˆ range n, X Ï‰ ^ i / i.factorial * t ^ i * exp (v * X Ï‰)]
        = âˆ‘ i âˆˆ range n, Î¼[fun Ï‰ â†¦ X Ï‰ ^ i * exp (v * X Ï‰)] / i.factorial * t ^ i := by
      rw [integral_finset_sum]
      Â· congr with n
        rw [â† integral_div, â† integral_mul_right]
        congr with Ï‰
        ring
      Â· exact fun i _ â†¦ h_int_pow i
    simp_rw [h_eq] at this
    suffices Tendsto (fun n â†¦
          âˆ‘ i âˆˆ range n, Î¼[fun Ï‰ â†¦ X Ï‰ ^ i * exp (v * X Ï‰)] / i.factorial * t ^ i)
        atTop (ğ“ (mgf X Î¼ (v + t))) by
      refine tendsto_nhds_unique this ?_
      refine HasSum.Multipliable.tendsto_sum_tsum_nat ?_
      exact summable_integral_pow_mul_exp_mul ht_int_pos ht_int_neg
    rwa [â† tendsto_const_sub_iff (b := mgf X Î¼ (v + t)), sub_self]
  have h_le n : |mgf X Î¼ (v + t)
        - Î¼[fun Ï‰ â†¦ âˆ‘ i âˆˆ range n, X Ï‰ ^ i / â†‘i.factorial * t ^ i * exp (v * X Ï‰)]|
      â‰¤ âˆ‘' i : {j // j âˆ‰ range n},
        Î¼[fun Ï‰ â†¦ |X Ï‰| ^ (i : â„•) * exp (v * X Ï‰)] / (i : â„•).factorial * |t| ^ (i : â„•) := by
    calc |mgf X Î¼ (v + t)
        - Î¼[fun Ï‰ â†¦ âˆ‘ i âˆˆ range n, X Ï‰ ^ i / â†‘i.factorial * t ^ i * exp (v * X Ï‰)]|
    _ = |Î¼[fun Ï‰ â†¦ âˆ‘' i : {j // j âˆ‰ range n},
        X Ï‰ ^ (i : â„•) / (i : â„•).factorial * t ^ (i : â„•) * exp (v * X Ï‰)]| := by
      simp_rw [mgf]
      rw [â† integral_sub ht_int_pos (integrable_finset_sum _ (fun i _ â†¦ h_int_pow i))]
      congr with Ï‰
      rw [add_mul, add_comm, exp_add, exp_eq_tsum, sub_eq_iff_eq_add']
      have : âˆ‘' n, (t * X Ï‰) ^ n / n.factorial = âˆ‘' n, X Ï‰ ^ n / n.factorial * t ^ n := by
        simp_rw [mul_pow]
        congr with n
        ring
      rw [this, â† tsum_mul_right]
      refine (sum_add_tsum_compl ?_).symm
      suffices Summable fun i â†¦ (t * X Ï‰) ^ i / i.factorial * exp (v * X Ï‰) by
        convert this using 2 with i
        ring
      exact Summable.mul_right _ <| summable_pow_div_factorial _
    _ = |âˆ‘' i : {j // j âˆ‰ range n},
        Î¼[fun Ï‰ â†¦ X Ï‰ ^ (i : â„•) * exp (v * X Ï‰)] / (i : â„•).factorial * t ^ (i : â„•)| := by
      have h_eq i Ï‰ : X Ï‰ ^ i / â†‘i.factorial * t ^ i * exp (v * X Ï‰)
          = X Ï‰ ^ i * exp (v * X Ï‰) / â†‘i.factorial * t ^ i := by ring
      simp_rw [h_eq]
      rw [â† integral_tsum_of_summable_integral_norm]
      Â· simp_rw [â† integral_div, â† integral_mul_right]
      Â· refine fun i â†¦ (Integrable.div_const ?_ _).mul_const _
        exact integrable_pow_mul_exp_of_integrable_exp_mul ht ht_int_pos ht_int_neg _
      Â· simp only [norm_mul, norm_div, norm_pow, norm_eq_abs, norm_natCast, Nat.abs_cast]
        simp_rw [integral_mul_right, integral_div, abs_exp]
        exact (summable_integral_pow_abs_mul_exp_mul_abs ht_int_pos ht_int_neg).subtype (range n)á¶œ
    _ â‰¤ âˆ‘' i : {j // j âˆ‰ range n},
        |Î¼[fun Ï‰ â†¦ X Ï‰ ^ (i : â„•) * exp (v * X Ï‰)] / (i : â„•).factorial * t ^ (i : â„•)| := by
      simp_rw [â† norm_eq_abs]
      refine norm_tsum_le_tsum_norm ?_
      rw [summable_norm_iff]
      exact (summable_integral_pow_mul_exp_mul ht_int_pos ht_int_neg).subtype (range n)á¶œ
    _ â‰¤ âˆ‘' i : {j // j âˆ‰ range n},
          Î¼[fun Ï‰ â†¦ |X Ï‰| ^ (i : â„•) * exp (v * X Ï‰)] / (i : â„•).factorial * |t| ^ (i : â„•) := by
      simp only [Pi.pow_apply]
      refine tsum_mono ?_ ?_ fun n â†¦ ?_
      Â· rw [summable_abs_iff]
        exact (summable_integral_pow_mul_exp_mul ht_int_pos ht_int_neg).subtype (range n)á¶œ
      Â· exact (summable_integral_pow_abs_mul_exp_mul_abs ht_int_pos ht_int_neg).subtype (range n)á¶œ
      Â· rw [abs_mul, abs_div, Nat.abs_cast, abs_pow]
        gcongr
        simp_rw [â† norm_eq_abs]
        refine (norm_integral_le_integral_norm _).trans ?_
        simp
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le (tendsto_const_nhds) ?_ ?_ h_le
  Â· refine (tendsto_tsum_compl_atTop_zero
      (fun i â†¦ Î¼[fun Ï‰ â†¦ |X Ï‰| ^ (i : â„•) * exp (v * X Ï‰)]
        / (i : â„•).factorial * |t| ^ (i : â„•))).comp ?_
    exact tendsto_finset_range
  Â· intro n
    positivity

lemma mgf_eq_tsum (ht_int_pos : Integrable (fun Ï‰ â†¦ rexp (t * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ rexp (- t * X Ï‰)) Î¼) :
    mgf X Î¼ t = âˆ‘' n, Î¼[X ^ n] / n.factorial * t ^ n := by
  have h := mgf_add_eq_tsum (Î¼ := Î¼) (X := X) (v := 0) (t := t) ?_ ?_
  Â· simpa using h
  Â· simpa using ht_int_pos
  Â· simpa using ht_int_neg

lemma mgf_abs_eq_tsum (ht_int_pos : Integrable (fun Ï‰ â†¦ rexp (t * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ rexp (- t * X Ï‰)) Î¼) :
    mgf (fun Ï‰ â†¦ |X Ï‰|) Î¼ t = âˆ‘' n, (Î¼[fun Ï‰ â†¦ |X Ï‰| ^ n]) / n.factorial * t ^ n := by
  refine mgf_eq_tsum (X := fun Ï‰ â†¦ |X Ï‰|) (Î¼ := Î¼) (t := t) ?_ ?_
  Â· exact integrable_exp_mul_abs ht_int_pos ht_int_neg
  Â· exact integrable_exp_mul_abs ht_int_neg (by simpa using ht_int_pos)

lemma hasFPowerSeriesOnBall_mgf [IsFiniteMeasure Î¼] (ht : t â‰  0)
    (ht_int_pos : Integrable (fun Ï‰ â†¦ rexp ((v + t) * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ rexp ((v - t) * X Ï‰)) Î¼) :
    HasFPowerSeriesOnBall (mgf X Î¼)
      (FormalMultilinearSeries.ofScalars â„
        (fun n â†¦ (Î¼[fun Ï‰ â†¦ X Ï‰ ^ n * exp (v * X Ï‰)] : â„) / n.factorial)) v â€–tâ€–â‚Š := by
  constructor
  Â· refine FormalMultilinearSeries.le_radius_of_summable _ ?_
    simp only [Pi.pow_apply, FormalMultilinearSeries.ofScalars_norm, norm_eq_abs,
      coe_nnnorm, abs_div, Nat.abs_cast]
    have h := summable_integral_pow_mul_exp_mul ht_int_pos ht_int_neg
    rw [â† summable_abs_iff] at h
    simp_rw [abs_mul, abs_div, abs_pow, Nat.abs_cast] at h
    exact h
  Â· simp [ht]
  Â· intro y hy
    simp_rw [FormalMultilinearSeries.ofScalars_apply_eq]
    simp only [Pi.pow_apply, smul_eq_mul, zero_add]
    simp only [Metric.emetric_ball_nnreal, coe_nnnorm, norm_eq_abs, Metric.mem_ball,
      dist_zero_right] at hy
    have hy_int_pos : Integrable (fun Ï‰ â†¦ rexp ((v + y) * X Ï‰)) Î¼ := by
      rcases le_total 0 t with ht | ht
      Â· rw [abs_of_nonneg ht, abs_lt] at hy
        refine integrable_exp_mul_of_le_of_le ht_int_neg ht_int_pos ?_ ?_
        Â· rw [sub_eq_add_neg]
          gcongr
          exact hy.1.le
        Â· gcongr
          exact hy.2.le
      Â· rw [abs_of_nonpos ht, abs_lt, neg_neg] at hy
        refine integrable_exp_mul_of_le_of_le ht_int_pos ht_int_neg ?_ ?_
        Â· gcongr
          exact hy.1.le
        Â· rw [sub_eq_add_neg]
          gcongr
          exact hy.2.le
    have hy_int_neg : Integrable (fun Ï‰ â†¦ rexp ((v - y) * X Ï‰)) Î¼ := by
      rcases le_total 0 t with ht | ht
      Â· rw [abs_of_nonneg ht, abs_lt] at hy
        refine integrable_exp_mul_of_le_of_le ht_int_neg ht_int_pos ?_ ?_
        Â· gcongr
          exact hy.2.le
        Â· rw [sub_eq_add_neg]
          gcongr
          rw [neg_le]
          exact hy.1.le
      Â· rw [abs_of_nonpos ht, abs_lt, neg_neg] at hy
        refine integrable_exp_mul_of_le_of_le ht_int_pos ht_int_neg ?_ ?_
        Â· rw [sub_eq_add_neg]
          gcongr
          rw [le_neg]
          exact hy.2.le
        Â· gcongr
          exact hy.1.le
    rw [Summable.hasSum_iff]
    Â· exact (mgf_add_eq_tsum hy_int_pos hy_int_neg).symm
    Â· exact summable_integral_pow_mul_exp_mul hy_int_pos hy_int_neg

lemma hasFPowerSeriesOnBall_mgf_zero [IsFiniteMeasure Î¼] (ht : t â‰  0)
    (ht_int_pos : Integrable (fun Ï‰ â†¦ rexp (t * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ rexp (-t * X Ï‰)) Î¼) :
    HasFPowerSeriesOnBall (mgf X Î¼)
      (FormalMultilinearSeries.ofScalars â„ (fun n â†¦ (Î¼[X ^ n] : â„) / n.factorial)) 0 â€–tâ€–â‚Š := by
  have h := hasFPowerSeriesOnBall_mgf ht ?_ ?_ (Î¼ := Î¼) (X := X) (v := 0)
  Â· simpa using h
  Â· simpa using ht_int_pos
  Â· simpa using ht_int_neg

lemma hasFPowerSeriesAt_mgf [IsFiniteMeasure Î¼] (ht : t â‰  0)
    (ht_int_pos : Integrable (fun Ï‰ â†¦ rexp ((v + t) * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ rexp ((v - t) * X Ï‰)) Î¼) :
    HasFPowerSeriesAt (mgf X Î¼)
      (FormalMultilinearSeries.ofScalars â„
        (fun n â†¦ (Î¼[fun Ï‰ â†¦ X Ï‰ ^ n * exp (v * X Ï‰)] : â„) / n.factorial)) v :=
  âŸ¨â€–tâ€–â‚Š, hasFPowerSeriesOnBall_mgf ht ht_int_pos ht_int_negâŸ©

lemma hasFPowerSeriesAt_mgf_of_mem_interior [IsFiniteMeasure Î¼]
    (hv : v âˆˆ interior {t | Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼}) :
    HasFPowerSeriesAt (mgf X Î¼)
      (FormalMultilinearSeries.ofScalars â„
        (fun n â†¦ (Î¼[fun Ï‰ â†¦ X Ï‰ ^ n * exp (v * X Ï‰)] : â„) / n.factorial)) v := by
  rw [mem_interior_iff_mem_nhds, mem_nhds_iff_exists_Ioo_subset] at hv
  obtain âŸ¨l, u, hvlu, h_subsetâŸ© := hv
  have ht : min (v - l) (u - v) / 2 â‰  0 := (ne_of_lt (by simpa)).symm
  exact hasFPowerSeriesAt_mgf ht (h_subset (add_half_inf_sub_mem_Ioo hvlu))
    (h_subset (sub_half_inf_sub_mem_Ioo hvlu))

lemma hasFPowerSeriesAt_mgf_zero [IsFiniteMeasure Î¼] (ht : t â‰  0)
    (ht_int_pos : Integrable (fun Ï‰ â†¦ rexp (t * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ rexp (-t * X Ï‰)) Î¼) :
    HasFPowerSeriesAt (mgf X Î¼)
      (FormalMultilinearSeries.ofScalars â„ (fun n â†¦ (Î¼[X ^ n] : â„) / n.factorial)) 0 :=
  âŸ¨â€–tâ€–â‚Š, hasFPowerSeriesOnBall_mgf_zero ht ht_int_pos ht_int_negâŸ©

lemma analyticAt_mgf [IsFiniteMeasure Î¼] (ht : t â‰  0)
    (ht_int_pos : Integrable (fun Ï‰ â†¦ rexp ((v + t) * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ rexp ((v - t) * X Ï‰)) Î¼) :
    AnalyticAt â„ (mgf X Î¼) v :=
  âŸ¨_, hasFPowerSeriesAt_mgf ht ht_int_pos ht_int_negâŸ©

lemma analyticAt_mgf_of_mem_interior [IsFiniteMeasure Î¼]
    (hv : v âˆˆ interior {t | Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼}) :
    AnalyticAt â„ (mgf X Î¼) v :=
  âŸ¨_, hasFPowerSeriesAt_mgf_of_mem_interior hvâŸ©

lemma analyticAt_mgf_zero [IsFiniteMeasure Î¼] (ht : t â‰  0)
    (ht_int_pos : Integrable (fun Ï‰ â†¦ rexp (t * X Ï‰)) Î¼)
    (ht_int_neg : Integrable (fun Ï‰ â†¦ rexp (-t * X Ï‰)) Î¼) :
    AnalyticAt â„ (mgf X Î¼) 0 :=
  âŸ¨_, hasFPowerSeriesAt_mgf_zero ht ht_int_pos ht_int_negâŸ©

/-- The moment generating function is analytic on the interior of the interval on which it is
defined. -/
lemma analyticOnNhd_mgf [IsFiniteMeasure Î¼] :
    AnalyticOnNhd â„ (mgf X Î¼) (interior {x | Integrable (fun Ï‰ â†¦ exp (x * X Ï‰)) Î¼}) :=
  fun _ hx â†¦ analyticAt_mgf_of_mem_interior hx

end Analytic

section MgfDeriv

variable [IsFiniteMeasure Î¼]

lemma deriv_mgf (h : v âˆˆ interior {t | Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼}) :
    deriv (mgf X Î¼) v = Î¼[fun Ï‰ â†¦ X Ï‰ * exp (v * X Ï‰)] := by
  simp [(hasFPowerSeriesAt_mgf_of_mem_interior h).deriv]

lemma deriv_mgf_zero (h : 0 âˆˆ interior {t | Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼}) :
    deriv (mgf X Î¼) 0 = Î¼[X] := by
  simp [deriv_mgf h]

/-- The nth derivative of the moment generating function of `X` at `v` in the interior of its
domain is `Î¼[X^n * exp(v * X)]`. -/
lemma iteratedDeriv_mgf (h : v âˆˆ interior {t | Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼}) (n : â„•) :
    iteratedDeriv n (mgf X Î¼) v = Î¼[fun Ï‰ â†¦ X Ï‰ ^ n * exp (v * X Ï‰)] := by
  rw [mem_interior_iff_mem_nhds, mem_nhds_iff_exists_Ioo_subset] at h
  obtain âŸ¨l, u, hvlu, h_subsetâŸ© := h
  have ht : min (v - l) (u - v) / 2 â‰  0 := (ne_of_lt (by simpa)).symm
  have h_series := hasFPowerSeriesOnBall_mgf ht (h_subset (add_half_inf_sub_mem_Ioo hvlu))
    (h_subset (sub_half_inf_sub_mem_Ioo hvlu))
  have h_fact_smul := h_series.factorial_smul 1 n
  simp only [FormalMultilinearSeries.apply_eq_prod_smul_coeff, prod_const_one,
    FormalMultilinearSeries.coeff_ofScalars, smul_eq_mul, one_mul, nsmul_eq_mul] at h_fact_smul
  rw [mul_div_cancelâ‚€] at h_fact_smul
  Â· exact h_fact_smul.symm
  Â· simp [n.factorial_ne_zero]

/-- The derivatives of the moment generating function at zero are the moments. -/
lemma iteratedDeriv_mgf_zero (h : 0 âˆˆ interior {t | Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼}) (n : â„•) :
    iteratedDeriv n (mgf X Î¼) 0 = Î¼[X ^ n] := by
  simp [iteratedDeriv_mgf h n]

end MgfDeriv

section CgfDeriv

variable [IsFiniteMeasure Î¼]

lemma deriv_cgf (h : v âˆˆ interior {t | Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼}) :
    deriv (cgf X Î¼) v = Î¼[fun Ï‰ â†¦ X Ï‰ * exp (v * X Ï‰)] / mgf X Î¼ v := by
  calc deriv (fun t â†¦ log (mgf X Î¼ t)) v
  _ = deriv (mgf X Î¼) v / mgf X Î¼ v := by
    sorry
  _ = Î¼[fun Ï‰ â†¦ X Ï‰ * rexp (v * X Ï‰)] / mgf X Î¼ v := by rw [deriv_mgf h]

lemma deriv_cgf_zero (h : 0 âˆˆ interior {t | Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼}) :
    deriv (cgf X Î¼) 0 = Î¼[X] / (Î¼ Set.univ).toReal := by
  simp [deriv_cgf h]

lemma iteratedDeriv_two_cgf (h : v âˆˆ interior {t | Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼}) :
    iteratedDeriv 2 (cgf X Î¼) v
      = Î¼[fun Ï‰ â†¦ (X Ï‰ - deriv (cgf X Î¼) v)^2 * exp (v * X Ï‰)] / mgf X Î¼ v := by
  sorry

lemma iteratedDeriv_three_cgf (h : v âˆˆ interior {t | Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼}) :
    iteratedDeriv 3 (cgf X Î¼) v
      = Î¼[fun Ï‰ â†¦ (X Ï‰ - deriv (cgf X Î¼) v)^3 * exp (v * X Ï‰)] / mgf X Î¼ v := by
  sorry

end CgfDeriv

section Convexity

lemma convexOn_cgf :
    ConvexOn â„ (interior {t | Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼}) (cgf X Î¼) := by
  sorry

/-- `2 cgf + log cgf''` is convex. -/
lemma convexOn_two_cgf_add_log_iteratedDeriv_two_cgf :
    ConvexOn â„ (interior {t | Integrable (fun Ï‰ â†¦ exp (t * X Ï‰)) Î¼})
      (fun v â†¦ 2 * cgf X Î¼ v + log (iteratedDeriv 2 (cgf X Î¼) v)) := by
  sorry

end Convexity

section IndepFun

/-- This is a trivial application of `IndepFun.comp` but it will come up frequently. -/
theorem IndepFun.exp_mul {X Y : Î© â†’ â„} (h_indep : IndepFun X Y Î¼) (s t : â„) :
    IndepFun (fun Ï‰ => exp (s * X Ï‰)) (fun Ï‰ => exp (t * Y Ï‰)) Î¼ := by
  have h_meas : âˆ€ t, Measurable fun x => exp (t * x) := fun t => (measurable_id'.const_mul t).exp
  change IndepFun ((fun x => exp (s * x)) âˆ˜ X) ((fun x => exp (t * x)) âˆ˜ Y) Î¼
  exact IndepFun.comp h_indep (h_meas s) (h_meas t)

theorem IndepFun.mgf_add {X Y : Î© â†’ â„} (h_indep : IndepFun X Y Î¼)
    (hX : AEStronglyMeasurable (fun Ï‰ => exp (t * X Ï‰)) Î¼)
    (hY : AEStronglyMeasurable (fun Ï‰ => exp (t * Y Ï‰)) Î¼) :
    mgf (X + Y) Î¼ t = mgf X Î¼ t * mgf Y Î¼ t := by
  simp_rw [mgf, Pi.add_apply, mul_add, exp_add]
  exact (h_indep.exp_mul t t).integral_mul hX hY

theorem IndepFun.mgf_add' {X Y : Î© â†’ â„} (h_indep : IndepFun X Y Î¼) (hX : AEStronglyMeasurable X Î¼)
    (hY : AEStronglyMeasurable Y Î¼) : mgf (X + Y) Î¼ t = mgf X Î¼ t * mgf Y Î¼ t := by
  have A : Continuous fun x : â„ => exp (t * x) := by fun_prop
  have h'X : AEStronglyMeasurable (fun Ï‰ => exp (t * X Ï‰)) Î¼ :=
    A.aestronglyMeasurable.comp_aemeasurable hX.aemeasurable
  have h'Y : AEStronglyMeasurable (fun Ï‰ => exp (t * Y Ï‰)) Î¼ :=
    A.aestronglyMeasurable.comp_aemeasurable hY.aemeasurable
  exact h_indep.mgf_add h'X h'Y

theorem IndepFun.cgf_add {X Y : Î© â†’ â„} (h_indep : IndepFun X Y Î¼)
    (h_int_X : Integrable (fun Ï‰ => exp (t * X Ï‰)) Î¼)
    (h_int_Y : Integrable (fun Ï‰ => exp (t * Y Ï‰)) Î¼) :
    cgf (X + Y) Î¼ t = cgf X Î¼ t + cgf Y Î¼ t := by
  by_cases hÎ¼ : Î¼ = 0
  Â· simp [hÎ¼]
  simp only [cgf, h_indep.mgf_add h_int_X.aestronglyMeasurable h_int_Y.aestronglyMeasurable]
  exact log_mul (mgf_pos' hÎ¼ h_int_X).ne' (mgf_pos' hÎ¼ h_int_Y).ne'

theorem aestronglyMeasurable_exp_mul_add {X Y : Î© â†’ â„}
    (h_int_X : AEStronglyMeasurable (fun Ï‰ => exp (t * X Ï‰)) Î¼)
    (h_int_Y : AEStronglyMeasurable (fun Ï‰ => exp (t * Y Ï‰)) Î¼) :
    AEStronglyMeasurable (fun Ï‰ => exp (t * (X + Y) Ï‰)) Î¼ := by
  simp_rw [Pi.add_apply, mul_add, exp_add]
  exact AEStronglyMeasurable.mul h_int_X h_int_Y

theorem aestronglyMeasurable_exp_mul_sum {X : Î¹ â†’ Î© â†’ â„} {s : Finset Î¹}
    (h_int : âˆ€ i âˆˆ s, AEStronglyMeasurable (fun Ï‰ => exp (t * X i Ï‰)) Î¼) :
    AEStronglyMeasurable (fun Ï‰ => exp (t * (âˆ‘ i âˆˆ s, X i) Ï‰)) Î¼ := by
  classical
  induction' s using Finset.induction_on with i s hi_notin_s h_rec h_int
  Â· simp only [Pi.zero_apply, sum_apply, sum_empty, mul_zero, exp_zero]
    exact aestronglyMeasurable_const
  Â· have : âˆ€ i : Î¹, i âˆˆ s â†’ AEStronglyMeasurable (fun Ï‰ : Î© => exp (t * X i Ï‰)) Î¼ := fun i hi =>
      h_int i (mem_insert_of_mem hi)
    specialize h_rec this
    rw [sum_insert hi_notin_s]
    apply aestronglyMeasurable_exp_mul_add (h_int i (mem_insert_self _ _)) h_rec

theorem IndepFun.integrable_exp_mul_add {X Y : Î© â†’ â„} (h_indep : IndepFun X Y Î¼)
    (h_int_X : Integrable (fun Ï‰ => exp (t * X Ï‰)) Î¼)
    (h_int_Y : Integrable (fun Ï‰ => exp (t * Y Ï‰)) Î¼) :
    Integrable (fun Ï‰ => exp (t * (X + Y) Ï‰)) Î¼ := by
  simp_rw [Pi.add_apply, mul_add, exp_add]
  exact (h_indep.exp_mul t t).integrable_mul h_int_X h_int_Y

theorem iIndepFun.integrable_exp_mul_sum [IsFiniteMeasure Î¼] {X : Î¹ â†’ Î© â†’ â„}
    (h_indep : iIndepFun (fun _ => inferInstance) X Î¼) (h_meas : âˆ€ i, Measurable (X i))
    {s : Finset Î¹} (h_int : âˆ€ i âˆˆ s, Integrable (fun Ï‰ => exp (t * X i Ï‰)) Î¼) :
    Integrable (fun Ï‰ => exp (t * (âˆ‘ i âˆˆ s, X i) Ï‰)) Î¼ := by
  classical
  induction' s using Finset.induction_on with i s hi_notin_s h_rec h_int
  Â· simp only [Pi.zero_apply, sum_apply, sum_empty, mul_zero, exp_zero]
    exact integrable_const _
  Â· have : âˆ€ i : Î¹, i âˆˆ s â†’ Integrable (fun Ï‰ : Î© => exp (t * X i Ï‰)) Î¼ := fun i hi =>
      h_int i (mem_insert_of_mem hi)
    specialize h_rec this
    rw [sum_insert hi_notin_s]
    refine IndepFun.integrable_exp_mul_add ?_ (h_int i (mem_insert_self _ _)) h_rec
    exact (h_indep.indepFun_finset_sum_of_not_mem h_meas hi_notin_s).symm

theorem iIndepFun.mgf_sum {X : Î¹ â†’ Î© â†’ â„}
    (h_indep : iIndepFun (fun _ => inferInstance) X Î¼) (h_meas : âˆ€ i, Measurable (X i))
    (s : Finset Î¹) : mgf (âˆ‘ i âˆˆ s, X i) Î¼ t = âˆ i âˆˆ s, mgf (X i) Î¼ t := by
  have : IsProbabilityMeasure Î¼ := h_indep.isProbabilityMeasure
  classical
  induction' s using Finset.induction_on with i s hi_notin_s h_rec h_int
  Â· simp only [sum_empty, mgf_zero_fun, measure_univ, ENNReal.one_toReal, prod_empty]
  Â· have h_int' : âˆ€ i : Î¹, AEStronglyMeasurable (fun Ï‰ : Î© => exp (t * X i Ï‰)) Î¼ := fun i =>
      ((h_meas i).const_mul t).exp.aestronglyMeasurable
    rw [sum_insert hi_notin_s,
      IndepFun.mgf_add (h_indep.indepFun_finset_sum_of_not_mem h_meas hi_notin_s).symm (h_int' i)
        (aestronglyMeasurable_exp_mul_sum fun i _ => h_int' i),
      h_rec, prod_insert hi_notin_s]

theorem iIndepFun.cgf_sum {X : Î¹ â†’ Î© â†’ â„}
    (h_indep : iIndepFun (fun _ => inferInstance) X Î¼) (h_meas : âˆ€ i, Measurable (X i))
    {s : Finset Î¹} (h_int : âˆ€ i âˆˆ s, Integrable (fun Ï‰ => exp (t * X i Ï‰)) Î¼) :
    cgf (âˆ‘ i âˆˆ s, X i) Î¼ t = âˆ‘ i âˆˆ s, cgf (X i) Î¼ t := by
  have : IsProbabilityMeasure Î¼ := h_indep.isProbabilityMeasure
  simp_rw [cgf]
  rw [â† log_prod _ _ fun j hj => ?_]
  Â· rw [h_indep.mgf_sum h_meas]
  Â· exact (mgf_pos (h_int j hj)).ne'

end IndepFun

section Chernoff

/-- **Chernoff bound** on the upper tail of a real random variable. -/
theorem measure_ge_le_exp_mul_mgf [IsFiniteMeasure Î¼] (Îµ : â„) (ht : 0 â‰¤ t)
    (h_int : Integrable (fun Ï‰ => exp (t * X Ï‰)) Î¼) :
    (Î¼ {Ï‰ | Îµ â‰¤ X Ï‰}).toReal â‰¤ exp (-t * Îµ) * mgf X Î¼ t := by
  rcases ht.eq_or_lt with ht_zero_eq | ht_pos
  Â· rw [ht_zero_eq.symm]
    simp only [neg_zero, zero_mul, exp_zero, mgf_zero', one_mul]
    gcongr
    exacts [measure_ne_top _ _, Set.subset_univ _]
  calc
    (Î¼ {Ï‰ | Îµ â‰¤ X Ï‰}).toReal = (Î¼ {Ï‰ | exp (t * Îµ) â‰¤ exp (t * X Ï‰)}).toReal := by
      congr with Ï‰
      simp only [Set.mem_setOf_eq, exp_le_exp, gt_iff_lt]
      exact âŸ¨fun h => mul_le_mul_of_nonneg_left h ht_pos.le,
        fun h => le_of_mul_le_mul_left h ht_posâŸ©
    _ â‰¤ (exp (t * Îµ))â»Â¹ * Î¼[fun Ï‰ => exp (t * X Ï‰)] := by
      have : exp (t * Îµ) * (Î¼ {Ï‰ | exp (t * Îµ) â‰¤ exp (t * X Ï‰)}).toReal â‰¤
          Î¼[fun Ï‰ => exp (t * X Ï‰)] :=
        mul_meas_ge_le_integral_of_nonneg (ae_of_all _ fun x => (exp_pos _).le) h_int _
      rwa [mul_comm (exp (t * Îµ))â»Â¹, â† div_eq_mul_inv, le_div_iffâ‚€' (exp_pos _)]
    _ = exp (-t * Îµ) * mgf X Î¼ t := by rw [neg_mul, exp_neg]; rfl

/-- **Chernoff bound** on the lower tail of a real random variable. -/
theorem measure_le_le_exp_mul_mgf [IsFiniteMeasure Î¼] (Îµ : â„) (ht : t â‰¤ 0)
    (h_int : Integrable (fun Ï‰ => exp (t * X Ï‰)) Î¼) :
    (Î¼ {Ï‰ | X Ï‰ â‰¤ Îµ}).toReal â‰¤ exp (-t * Îµ) * mgf X Î¼ t := by
  rw [â† neg_neg t, â† mgf_neg, neg_neg, â† neg_mul_neg (-t)]
  refine Eq.trans_le ?_ (measure_ge_le_exp_mul_mgf (-Îµ) (neg_nonneg.mpr ht) ?_)
  Â· congr with Ï‰
    simp only [Pi.neg_apply, neg_le_neg_iff]
  Â· simp_rw [Pi.neg_apply, neg_mul_neg]
    exact h_int

/-- **Chernoff bound** on the upper tail of a real random variable. -/
theorem measure_ge_le_exp_cgf [IsFiniteMeasure Î¼] (Îµ : â„) (ht : 0 â‰¤ t)
    (h_int : Integrable (fun Ï‰ => exp (t * X Ï‰)) Î¼) :
    (Î¼ {Ï‰ | Îµ â‰¤ X Ï‰}).toReal â‰¤ exp (-t * Îµ + cgf X Î¼ t) := by
  refine (measure_ge_le_exp_mul_mgf Îµ ht h_int).trans ?_
  rw [exp_add]
  exact mul_le_mul le_rfl (le_exp_log _) mgf_nonneg (exp_pos _).le

/-- **Chernoff bound** on the lower tail of a real random variable. -/
theorem measure_le_le_exp_cgf [IsFiniteMeasure Î¼] (Îµ : â„) (ht : t â‰¤ 0)
    (h_int : Integrable (fun Ï‰ => exp (t * X Ï‰)) Î¼) :
    (Î¼ {Ï‰ | X Ï‰ â‰¤ Îµ}).toReal â‰¤ exp (-t * Îµ + cgf X Î¼ t) := by
  refine (measure_le_le_exp_mul_mgf Îµ ht h_int).trans ?_
  rw [exp_add]
  exact mul_le_mul le_rfl (le_exp_log _) mgf_nonneg (exp_pos _).le

end Chernoff

end MomentGeneratingFunction

end ProbabilityTheory
