/-
Copyright (c) 2022 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import Mathlib.Probability.Martingale.BorelCantelli
import Mathlib.Probability.ConditionalExpectation
import Mathlib.Probability.Independence.Basic

/-!

# The second Borel-Cantelli lemma

This file contains the *second Borel-Cantelli lemma* which states that, given a sequence of
independent sets `(s‚Çô)` in a probability space, if `‚àë n, Œº s‚Çô = ‚àû`, then the limsup of `s‚Çô` has
measure 1. We employ a proof using L√©vy's generalized Borel-Cantelli by choosing an appropriate
filtration.

## Main result

- `ProbabilityTheory.measure_limsup_eq_one`: the second Borel-Cantelli lemma.

**Note**: for the *first Borel-Cantelli lemma*, which holds in general measure spaces (not only
in probability spaces), see `MeasureTheory.measure_limsup_atTop_eq_zero`.
-/

open scoped ENNReal Topology
open MeasureTheory

namespace ProbabilityTheory

variable {Œ© : Type*} {m0 : MeasurableSpace Œ©} {Œº : Measure Œ©}

section BorelCantelli

variable {Œπ Œ≤ : Type*} [LinearOrder Œπ] [mŒ≤ : MeasurableSpace Œ≤] [NormedAddCommGroup Œ≤]
  [BorelSpace Œ≤] {f : Œπ ‚Üí Œ© ‚Üí Œ≤} {i j : Œπ} {s : Œπ ‚Üí Set Œ©}

theorem iIndepFun.indep_comap_natural_of_lt (hf : ‚àÄ i, StronglyMeasurable (f i))
    (hfi : iIndepFun f Œº) (hij : i < j) :
    Indep (MeasurableSpace.comap (f j) mŒ≤) (Filtration.natural f hf i) Œº := by
  suffices Indep (‚®Ü k ‚àà ({j} : Set Œπ), MeasurableSpace.comap (f k) mŒ≤)
      (‚®Ü k ‚àà {k | k ‚â§ i}, MeasurableSpace.comap (f k) mŒ≤) Œº by rwa [iSup_singleton] at this
  exact indep_iSup_of_disjoint (fun k => (hf k).measurable.comap_le) hfi (by simpa)

theorem iIndepFun.condExp_natural_ae_eq_of_lt [SecondCountableTopology Œ≤] [CompleteSpace Œ≤]
    [NormedSpace ‚Ñù Œ≤] (hf : ‚àÄ i, StronglyMeasurable (f i)) (hfi : iIndepFun f Œº)
    (hij : i < j) : Œº[f j|Filtration.natural f hf i] =·µê[Œº] fun _ => Œº[f j] := by
  have : IsProbabilityMeasure Œº := hfi.isProbabilityMeasure
  exact condExp_indep_eq (hf j).measurable.comap_le (Filtration.le _ _)
    (comap_measurable <| f j).stronglyMeasurable (hfi.indep_comap_natural_of_lt hf hij)

theorem iIndepSet.condExp_indicator_filtrationOfSet_ae_eq (hsm : ‚àÄ n, MeasurableSet (s n))
    (hs : iIndepSet s Œº) (hij : i < j) :
    Œº[(s j).indicator (fun _ => 1 : Œ© ‚Üí ‚Ñù)|filtrationOfSet hsm i] =·µê[Œº]
    fun _ => Œº.real (s j) := by
  rw [Filtration.filtrationOfSet_eq_natural (Œ≤ := fun _ ‚Ü¶ ‚Ñù) hsm]
  refine (iIndepFun.condExp_natural_ae_eq_of_lt _ hs.iIndepFun_indicator hij).trans ?_
  simp only [integral_indicator_const _ (hsm _), Algebra.id.smul_eq_mul, mul_one]; rfl

open Filter

/-- **The second Borel-Cantelli lemma**: Given a sequence of independent sets `(s‚Çô)` such that
`‚àë n, Œº s‚Çô = ‚àû`, `limsup s‚Çô` has measure 1. -/
theorem measure_limsup_eq_one {s : ‚Ñï ‚Üí Set Œ©} (hsm : ‚àÄ n, MeasurableSet (s n)) (hs : iIndepSet s Œº)
    (hs' : (‚àë' n, Œº (s n)) = ‚àû) : Œº (limsup s atTop) = 1 := by
  have : IsProbabilityMeasure Œº := hs.isProbabilityMeasure
  rw [measure_congr (eventuallyEq_set.2 (ae_mem_limsup_atTop_iff Œº <|
    measurableSet_filtrationOfSet' hsm) : (limsup s atTop : Set Œ©) =·µê[Œº]
      {œâ | Tendsto (fun n => ‚àë k ‚àà Finset.range n,
        (Œº[(s (k + 1)).indicator (1 : Œ© ‚Üí ‚Ñù)|filtrationOfSet hsm k]) œâ) atTop atTop})]
  suffices {œâ | Tendsto (fun n => ‚àë k ‚àà Finset.range n,
      (Œº[(s (k + 1)).indicator (1 : Œ© ‚Üí ‚Ñù)|filtrationOfSet hsm k]) œâ) atTop atTop} =·µê[Œº] Set.univ by
    rw [measure_congr this, measure_univ]
  have : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ n, (Œº[(s (n + 1)).indicator (1 : Œ© ‚Üí ‚Ñù)|filtrationOfSet hsm n]) œâ = _ :=
    ae_all_iff.2 fun n => hs.condExp_indicator_filtrationOfSet_ae_eq hsm n.lt_succ_self
  filter_upwards [this] with œâ hœâ
  refine eq_true (?_ : Tendsto _ _ _)
  simp_rw [hœâ]
  have htends : Tendsto (fun n => ‚àë k ‚àà Finset.range n, Œº (s (k + 1))) atTop (ùìù ‚àû) := by
    rw [‚Üê ENNReal.tsum_add_one_eq_top hs' (measure_ne_top _ _)]
    exact ENNReal.tendsto_nat_tsum _
  rw [ENNReal.tendsto_nhds_top_iff_nnreal] at htends
  refine tendsto_atTop_atTop_of_monotone' ?_ ?_
  ¬∑ refine monotone_nat_of_le_succ fun n => ?_
    rw [‚Üê sub_nonneg, Finset.sum_range_succ_sub_sum]
    exact ENNReal.toReal_nonneg
  ¬∑ rintro ‚ü®B, hB‚ü©
    refine not_eventually.2 (Frequently.of_forall fun n => ?_) (htends B.toNNReal)
    rw [mem_upperBounds] at hB
    specialize hB (‚àë k ‚àà Finset.range n, Œº (s (k + 1))).toReal _
    ¬∑ refine ‚ü®n, ?_‚ü©
      rw [ENNReal.toReal_sum (by finiteness)]
      rfl
    ¬∑ rwa [not_lt, ENNReal.ofNNReal_toNNReal, ENNReal.le_ofReal_iff_toReal_le]
      ¬∑ simp
      ¬∑ exact le_trans (by positivity) hB

end BorelCantelli

end ProbabilityTheory
