/-
Copyright (c) 2022 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import Mathlib.Probability.Martingale.BorelCantelli
import Mathlib.Probability.ConditionalExpectation
import Mathlib.Probability.Independence.Basic

/-!

# The second Borel-Cantelli lemma

This file contains the *second Borel-Cantelli lemma* which states that, given a sequence of
independent sets `(sâ‚™)` in a probability space, if `âˆ‘ n, Î¼ sâ‚™ = âˆ`, then the limsup of `sâ‚™` has
measure 1. We employ a proof using LÃ©vy's generalized Borel-Cantelli by choosing an appropriate
filtration.

## Main result

- `ProbabilityTheory.measure_limsup_eq_one`: the second Borel-Cantelli lemma.

**Note**: for the *first Borel-Cantelli lemma*, which holds in general measure spaces (not only
in probability spaces), see `MeasureTheory.measure_limsup_eq_zero`.
-/

open scoped ENNReal Topology
open MeasureTheory

namespace ProbabilityTheory

variable {Î© : Type*} {m0 : MeasurableSpace Î©} {Î¼ : Measure Î©} [IsProbabilityMeasure Î¼]

section BorelCantelli

variable {Î¹ Î² : Type*} [LinearOrder Î¹] [mÎ² : MeasurableSpace Î²] [NormedAddCommGroup Î²]
  [BorelSpace Î²] {f : Î¹ â†’ Î© â†’ Î²} {i j : Î¹} {s : Î¹ â†’ Set Î©}

theorem iIndepFun.indep_comap_natural_of_lt (hf : âˆ€ i, StronglyMeasurable (f i))
    (hfi : iIndepFun (fun _ => mÎ²) f Î¼) (hij : i < j) :
    Indep (MeasurableSpace.comap (f j) mÎ²) (Filtration.natural f hf i) Î¼ := by
  suffices Indep (â¨† k âˆˆ ({j} : Set Î¹), MeasurableSpace.comap (f k) mÎ²)
      (â¨† k âˆˆ {k | k â‰¤ i}, MeasurableSpace.comap (f k) mÎ²) Î¼ by rwa [iSup_singleton] at this
  exact indep_iSup_of_disjoint (fun k => (hf k).measurable.comap_le) hfi (by simpa)

theorem iIndepFun.condexp_natural_ae_eq_of_lt [SecondCountableTopology Î²] [CompleteSpace Î²]
    [NormedSpace â„ Î²] (hf : âˆ€ i, StronglyMeasurable (f i)) (hfi : iIndepFun (fun _ => mÎ²) f Î¼)
    (hij : i < j) : Î¼[f j|Filtration.natural f hf i] =áµ[Î¼] fun _ => Î¼[f j] :=
  condexp_indep_eq (hf j).measurable.comap_le (Filtration.le _ _)
    (comap_measurable <| f j).stronglyMeasurable (hfi.indep_comap_natural_of_lt hf hij)

theorem iIndepSet.condexp_indicator_filtrationOfSet_ae_eq (hsm : âˆ€ n, MeasurableSet (s n))
    (hs : iIndepSet s Î¼) (hij : i < j) :
    Î¼[(s j).indicator (fun _ => 1 : Î© â†’ â„)|filtrationOfSet hsm i] =áµ[Î¼]
    fun _ => (Î¼ (s j)).toReal := by
  rw [Filtration.filtrationOfSet_eq_natural (Î² := â„) hsm]
  refine (iIndepFun.condexp_natural_ae_eq_of_lt _ hs.iIndepFun_indicator hij).trans ?_
  simp only [integral_indicator_const _ (hsm _), Algebra.id.smul_eq_mul, mul_one]; rfl

open Filter

/-- **The second Borel-Cantelli lemma**: Given a sequence of independent sets `(sâ‚™)` such that
`âˆ‘ n, Î¼ sâ‚™ = âˆ`, `limsup sâ‚™` has measure 1. -/
theorem measure_limsup_eq_one {s : â„• â†’ Set Î©} (hsm : âˆ€ n, MeasurableSet (s n)) (hs : iIndepSet s Î¼)
    (hs' : (âˆ‘' n, Î¼ (s n)) = âˆ) : Î¼ (limsup s atTop) = 1 := by
  rw [measure_congr (eventuallyEq_set.2 (ae_mem_limsup_atTop_iff Î¼ <|
    measurableSet_filtrationOfSet' hsm) : (limsup s atTop : Set Î©) =áµ[Î¼]
      {Ï‰ | Tendsto (fun n => âˆ‘ k âˆˆ Finset.range n,
        (Î¼[(s (k + 1)).indicator (1 : Î© â†’ â„)|filtrationOfSet hsm k]) Ï‰) atTop atTop})]
  suffices {Ï‰ | Tendsto (fun n => âˆ‘ k âˆˆ Finset.range n,
      (Î¼[(s (k + 1)).indicator (1 : Î© â†’ â„)|filtrationOfSet hsm k]) Ï‰) atTop atTop} =áµ[Î¼] Set.univ by
    rw [measure_congr this, measure_univ]
  have : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ n, (Î¼[(s (n + 1)).indicator (1 : Î© â†’ â„)|filtrationOfSet hsm n]) Ï‰ = _ :=
    ae_all_iff.2 fun n => hs.condexp_indicator_filtrationOfSet_ae_eq hsm n.lt_succ_self
  filter_upwards [this] with Ï‰ hÏ‰
  refine eq_true (?_ : Tendsto _ _ _)
  simp_rw [hÏ‰]
  have htends : Tendsto (fun n => âˆ‘ k âˆˆ Finset.range n, Î¼ (s (k + 1))) atTop (ğ“ âˆ) := by
    rw [â† ENNReal.tsum_add_one_eq_top hs' (measure_ne_top _ _)]
    exact ENNReal.tendsto_nat_tsum _
  rw [ENNReal.tendsto_nhds_top_iff_nnreal] at htends
  refine tendsto_atTop_atTop_of_monotone' ?_ ?_
  Â· refine monotone_nat_of_le_succ fun n => ?_
    rw [â† sub_nonneg, Finset.sum_range_succ_sub_sum]
    exact ENNReal.toReal_nonneg
  Â· rintro âŸ¨B, hBâŸ©
    refine not_eventually.2 (frequently_of_forall fun n => ?_) (htends B.toNNReal)
    rw [mem_upperBounds] at hB
    specialize hB (âˆ‘ k âˆˆ Finset.range n, Î¼ (s (k + 1))).toReal _
    Â· refine âŸ¨n, ?_âŸ©
      rw [ENNReal.toReal_sum]
      exact fun _ _ => measure_ne_top _ _
    Â· rw [not_lt, â† ENNReal.toReal_le_toReal (ENNReal.sum_lt_top _).ne ENNReal.coe_ne_top]
      Â· exact hB.trans (by simp)
      Â· exact fun _ _ => measure_ne_top _ _

end BorelCantelli

end ProbabilityTheory
