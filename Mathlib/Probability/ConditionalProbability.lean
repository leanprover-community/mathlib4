/-
Copyright (c) 2022 Rishikesh Vaishnav. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Rishikesh Vaishnav
-/
import Mathlib.MeasureTheory.Measure.MeasureSpace

#align_import probability.conditional_probability from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!
# Conditional Probability

This file defines conditional probability and includes basic results relating to it.

Given some measure `Î¼` defined on a measure space on some type `Î©` and some `s : Set Î©`,
we define the measure of `Î¼` conditioned on `s` as the restricted measure scaled by
the inverse of the measure of `s`: `cond Î¼ s = (Î¼ s)â»Â¹ â€¢ Î¼.restrict s`. The scaling
ensures that this is a probability measure (when `Î¼` is a finite measure).

From this definition, we derive the "axiomatic" definition of conditional probability
based on application: for any `s t : Set Î©`, we have `Î¼[t|s] = (Î¼ s)â»Â¹ * Î¼ (s âˆ© t)`.

## Main Statements

* `cond_cond_eq_cond_inter`: conditioning on one set and then another is equivalent
  to conditioning on their intersection.
* `cond_eq_inv_mul_cond_mul`: Bayes' Theorem, `Î¼[t|s] = (Î¼ s)â»Â¹ * Î¼[s|t] * (Î¼ t)`.

## Notations

This file uses the notation `Î¼[|s]` the measure of `Î¼` conditioned on `s`,
and `Î¼[t|s]` for the probability of `t` given `s` under `Î¼` (equivalent to the
application `Î¼[|s] t`).

These notations are contained in the locale `ProbabilityTheory`.

## Implementation notes

Because we have the alternative measure restriction application principles
`Measure.restrict_apply` and `Measure.restrict_apply'`, which require
measurability of the restricted and restricting sets, respectively,
many of the theorems here will have corresponding alternatives as well.
For the sake of brevity, we've chosen to only go with `Measure.restrict_apply'`
for now, but the alternative theorems can be added if needed.

Use of `@[simp]` generally follows the rule of removing conditions on a measure
when possible.

Hypotheses that are used to "define" a conditional distribution by requiring that
the conditioning set has non-zero measure should be named using the abbreviation
"c" (which stands for "conditionable") rather than "nz". For example `(hci : Î¼ (s âˆ© t) â‰  0)`
(rather than `hnzi`) should be used for a hypothesis ensuring that `Î¼[|s âˆ© t]` is defined.

## Tags
conditional, conditioned, bayes
-/


noncomputable section

open ENNReal MeasureTheory MeasurableSpace

variable {Î© : Type*} {m : MeasurableSpace Î©} (Î¼ : Measure Î©) {s t : Set Î©}

namespace ProbabilityTheory

section Definitions

/-- The conditional probability measure of measure `Î¼` on set `s` is `Î¼` restricted to `s`
and scaled by the inverse of `Î¼ s` (to make it a probability measure):
`(Î¼ s)â»Â¹ â€¢ Î¼.restrict s`. -/
def cond (s : Set Î©) : Measure Î© :=
  (Î¼ s)â»Â¹ â€¢ Î¼.restrict s
#align probability_theory.cond ProbabilityTheory.cond

end Definitions

scoped notation Î¼ "[" s "|" t "]" => ProbabilityTheory.cond Î¼ t s
scoped notation:60 Î¼ "[|" t "]" => ProbabilityTheory.cond Î¼ t

/-- The conditional probability measure of any finite measure on any set of positive measure
is a probability measure. -/
theorem cond_isProbabilityMeasure [IsFiniteMeasure Î¼] (hcs : Î¼ s â‰  0) :
    IsProbabilityMeasure (Î¼[|s]) :=
  âŸ¨by
    rw [cond, Measure.smul_apply, Measure.restrict_apply MeasurableSet.univ, Set.univ_inter]
    -- âŠ¢ (â†‘â†‘Î¼ s)â»Â¹ â€¢ â†‘â†‘Î¼ s = 1
    exact ENNReal.inv_mul_cancel hcs (measure_ne_top _ s)âŸ©
    -- ğŸ‰ no goals
#align probability_theory.cond_is_probability_measure ProbabilityTheory.cond_isProbabilityMeasure

section Bayes

@[simp]
theorem cond_empty : Î¼[|âˆ…] = 0 := by simp [cond]
                                     -- ğŸ‰ no goals
#align probability_theory.cond_empty ProbabilityTheory.cond_empty

@[simp]
theorem cond_univ [IsProbabilityMeasure Î¼] : Î¼[|Set.univ] = Î¼ := by
  simp [cond, measure_univ, Measure.restrict_univ]
  -- ğŸ‰ no goals
#align probability_theory.cond_univ ProbabilityTheory.cond_univ

/-- The axiomatic definition of conditional probability derived from a measure-theoretic one. -/
theorem cond_apply (hms : MeasurableSet s) (t : Set Î©) : Î¼[t|s] = (Î¼ s)â»Â¹ * Î¼ (s âˆ© t) := by
  rw [cond, Measure.smul_apply, Measure.restrict_apply' hms, Set.inter_comm, smul_eq_mul]
  -- ğŸ‰ no goals
#align probability_theory.cond_apply ProbabilityTheory.cond_apply

theorem cond_inter_self (hms : MeasurableSet s) (t : Set Î©) : Î¼[s âˆ© t|s] = Î¼[t|s] := by
  rw [cond_apply _ hms, â† Set.inter_assoc, Set.inter_self, â† cond_apply _ hms]
  -- ğŸ‰ no goals
#align probability_theory.cond_inter_self ProbabilityTheory.cond_inter_self

theorem inter_pos_of_cond_ne_zero (hms : MeasurableSet s) (hcst : Î¼[t|s] â‰  0) : 0 < Î¼ (s âˆ© t) := by
  refine' pos_iff_ne_zero.mpr (right_ne_zero_of_mul _)
  -- âŠ¢ â„â‰¥0âˆ
  Â· exact (Î¼ s)â»Â¹
    -- ğŸ‰ no goals
  convert hcst
  -- âŠ¢ (â†‘â†‘Î¼ s)â»Â¹ * â†‘â†‘Î¼ (s âˆ© t) = â†‘â†‘(Î¼[|s]) t
  simp [hms, Set.inter_comm, cond]
  -- ğŸ‰ no goals
#align probability_theory.inter_pos_of_cond_ne_zero ProbabilityTheory.inter_pos_of_cond_ne_zero

theorem cond_pos_of_inter_ne_zero [IsFiniteMeasure Î¼]
    (hms : MeasurableSet s) (hci : Î¼ (s âˆ© t) â‰  0) : 0 < (Î¼[|s]) t := by
  rw [cond_apply _ hms]
  -- âŠ¢ 0 < (â†‘â†‘Î¼ s)â»Â¹ * â†‘â†‘Î¼ (s âˆ© t)
  refine' ENNReal.mul_pos _ hci
  -- âŠ¢ (â†‘â†‘Î¼ s)â»Â¹ â‰  0
  exact ENNReal.inv_ne_zero.mpr (measure_ne_top _ _)
  -- ğŸ‰ no goals
#align probability_theory.cond_pos_of_inter_ne_zero ProbabilityTheory.cond_pos_of_inter_ne_zero

theorem cond_cond_eq_cond_inter' (hms : MeasurableSet s) (hmt : MeasurableSet t) (hcs : Î¼ s â‰  âˆ)
    (hci : Î¼ (s âˆ© t) â‰  0) : Î¼[|s][|t] = Î¼[|s âˆ© t] := by
  have hcs' : Î¼ s â‰  0 :=
    (Î¼.toOuterMeasure.pos_of_subset_ne_zero (Set.inter_subset_left _ _) hci).ne'
  ext u
  -- âŠ¢ â†‘â†‘(Î¼[|s][|t]) u = â†‘â†‘(Î¼[|s âˆ© t]) u
  simp [*, hms.inter hmt, cond_apply, â† mul_assoc, â† Set.inter_assoc, ENNReal.mul_inv, mul_comm, â†
    mul_assoc, ENNReal.mul_inv_cancel]
#align probability_theory.cond_cond_eq_cond_inter' ProbabilityTheory.cond_cond_eq_cond_inter'

/-- Conditioning first on `s` and then on `t` results in the same measure as conditioning
on `s âˆ© t`. -/
theorem cond_cond_eq_cond_inter [IsFiniteMeasure Î¼] (hms : MeasurableSet s) (hmt : MeasurableSet t)
    (hci : Î¼ (s âˆ© t) â‰  0) : Î¼[|s][|t] = Î¼[|s âˆ© t] :=
  cond_cond_eq_cond_inter' Î¼ hms hmt (measure_ne_top Î¼ s) hci
#align probability_theory.cond_cond_eq_cond_inter ProbabilityTheory.cond_cond_eq_cond_inter

theorem cond_mul_eq_inter' (hms : MeasurableSet s) (hcs : Î¼ s â‰  0) (hcs' : Î¼ s â‰  âˆ) (t : Set Î©) :
    Î¼[t|s] * Î¼ s = Î¼ (s âˆ© t) := by
  rw [cond_apply Î¼ hms t, mul_comm, â† mul_assoc, ENNReal.mul_inv_cancel hcs hcs', one_mul]
  -- ğŸ‰ no goals
#align probability_theory.cond_mul_eq_inter' ProbabilityTheory.cond_mul_eq_inter'

theorem cond_mul_eq_inter [IsFiniteMeasure Î¼] (hms : MeasurableSet s) (hcs : Î¼ s â‰  0) (t : Set Î©) :
    Î¼[t|s] * Î¼ s = Î¼ (s âˆ© t) :=
  cond_mul_eq_inter' Î¼ hms hcs (measure_ne_top _ s) t
#align probability_theory.cond_mul_eq_inter ProbabilityTheory.cond_mul_eq_inter

/-- A version of the law of total probability. -/
theorem cond_add_cond_compl_eq [IsFiniteMeasure Î¼] (hms : MeasurableSet s) (hcs : Î¼ s â‰  0)
    (hcs' : Î¼ sá¶œ â‰  0) : Î¼[t|s] * Î¼ s + Î¼[t|sá¶œ] * Î¼ sá¶œ = Î¼ t := by
  rw [cond_mul_eq_inter Î¼ hms hcs, cond_mul_eq_inter Î¼ hms.compl hcs', Set.inter_comm _ t,
    Set.inter_comm _ t]
  exact measure_inter_add_diff t hms
  -- ğŸ‰ no goals
#align probability_theory.cond_add_cond_compl_eq ProbabilityTheory.cond_add_cond_compl_eq

/-- **Bayes' Theorem** -/
theorem cond_eq_inv_mul_cond_mul [IsFiniteMeasure Î¼]
    (hms : MeasurableSet s) (hmt : MeasurableSet t) : Î¼[t|s] = (Î¼ s)â»Â¹ * Î¼[s|t] * Î¼ t := by
  by_cases ht : Î¼ t = 0
  -- âŠ¢ â†‘â†‘(Î¼[|s]) t = (â†‘â†‘Î¼ s)â»Â¹ * â†‘â†‘(Î¼[|t]) s * â†‘â†‘Î¼ t
  Â· simp [cond, ht, Measure.restrict_apply hmt, Or.inr (measure_inter_null_of_null_left s ht)]
    -- ğŸ‰ no goals
  Â· rw [mul_assoc, cond_mul_eq_inter Î¼ hmt ht s, Set.inter_comm, cond_apply _ hms]
    -- ğŸ‰ no goals
#align probability_theory.cond_eq_inv_mul_cond_mul ProbabilityTheory.cond_eq_inv_mul_cond_mul

end Bayes

end ProbabilityTheory
