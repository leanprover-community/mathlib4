/-
Copyright (c) 2023 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import Mathlib.Probability.Kernel.CondCdf

/-!
# Cumulative distribution function of a real probability measure

The cumulative distribution function (CDF) of a probability measure over `‚Ñù` is a monotone, right
continuous function with limit 0 at -‚àû and 1 at +‚àû. Two probability measures are equal if and only
if they have the same CDF.

## Main definitions

* `ProbabilityTheory.CDF Œº`: cumulative distribution function of `Œº : Measure ‚Ñù`, defined as the
  conditional CDF (`ProbabilityTheory.condCdf`) of the product measure
  `(Measure.dirac Unit.unit).prod Œº` evaluated at `Unit.unit`.

## Main statements

* `ProbabilityTheory.ofReal_cdf`: for a probability measure `Œº` and `x : ‚Ñù`,
  `ENNReal.ofReal (CDF Œº x) = Œº (Iic x)`.
* `MeasureTheory.Measure.ext_of_cdf`: two probability measures are equal if and only if they have
  the same CDF.

## TODO

The definition could be extended to a finite measure by rescaling `condCdf`, but it would be nice
to have more structure on Stieltjes functions first. Right now, if `f` is a Stieltjes function,
`2 ‚Ä¢ f` makes no sense. We could define Stieltjes functions as a submodule.

The definition could be extended to `‚Ñù‚Åø`, either by extending the definition of `condCdf`, or by
using another construction here.
-/

open MeasureTheory Set Filter

open scoped Topology

namespace ProbabilityTheory

/-- Cumulative distribution function of a real measure. -/
noncomputable
def CDF (Œº : Measure ‚Ñù) : StieltjesFunction :=
  condCdf ((Measure.dirac Unit.unit).prod Œº) Unit.unit

variable {Œº : Measure ‚Ñù}

/-- The CDF is non-negative. -/
lemma cdf_nonneg (x : ‚Ñù) : 0 ‚â§ CDF Œº x := condCdf_nonneg _ _ _

/-- The CDF is lower or equal to 1. -/
lemma cdf_le_one (x : ‚Ñù) : CDF Œº x ‚â§ 1 := condCdf_le_one _ _ _

/-- The CDF is monotone. -/
lemma monotone_cdf : Monotone (CDF Œº) := (condCdf _ _).mono

/-- The CDF tends to 0 at -‚àû. -/
lemma tendsto_cdf_atBot : Tendsto (CDF Œº) atBot (ùìù 0) := tendsto_condCdf_atBot _ _

/-- The CDF tends to 1 at +‚àû. -/
lemma tendsto_cdf_atTop : Tendsto (CDF Œº) atTop (ùìù 1) := tendsto_condCdf_atTop _ _

lemma ofReal_cdf [IsProbabilityMeasure Œº] (x : ‚Ñù) : ENNReal.ofReal (CDF Œº x) = Œº (Iic x) := by
  have h := lintegral_condCdf ((Measure.dirac Unit.unit).prod Œº) x
  simpa only [MeasureTheory.Measure.fst_prod, Measure.prod_prod, measure_univ, one_mul,
    lintegral_dirac] using h

instance instIsProbabilityMeasureCDF : IsProbabilityMeasure (CDF Œº).measure := by
  constructor
  simp only [StieltjesFunction.measure_univ _ tendsto_cdf_atBot tendsto_cdf_atTop, sub_zero,
    ENNReal.ofReal_one]

/-- The measure associated to the CDF of a probability measure is the same probability measure. -/
lemma measure_cdf [IsProbabilityMeasure Œº] : (CDF Œº).measure = Œº := by
  refine Measure.ext_of_Iic (CDF Œº).measure Œº (fun a ‚Ü¶ ?_)
  rw [StieltjesFunction.measure_Iic _ tendsto_cdf_atBot, sub_zero, ofReal_cdf]

end ProbabilityTheory

open ProbabilityTheory

/-- If two real probability distributions have the same CDF, they are equal. -/
lemma MeasureTheory.Measure.ext_of_cdf (Œº ŒΩ : Measure ‚Ñù) [IsProbabilityMeasure Œº]
    [IsProbabilityMeasure ŒΩ] (h : CDF Œº = CDF ŒΩ) : Œº = ŒΩ := by
  rw [‚Üê @measure_cdf Œº, ‚Üê @measure_cdf ŒΩ, h]
