/-
Copyright (c) 2023 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Decomposition.RadonNikodym
import Mathlib.Probability.Kernel.Disintegration.CdfToKernel
import Mathlib.Probability.Kernel.Disintegration.AuxLemmas

#align_import probability.kernel.cond_cdf from "leanprover-community/mathlib"@"3b88f4005dc2e28d42f974cc1ce838f0dafb39b8"

/-!
# Conditional cumulative distribution function

Given `Ï : Measure (Î± Ã— â„)`, we define the conditional cumulative distribution function
(conditional cdf) of `Ï`. It is a function `condCDF Ï : Î± â†’ â„ â†’ â„` such that if `Ï` is a finite
measure, then for all `a : Î±` `condCDF Ï a` is monotone and right-continuous with limit 0 at -âˆ
and limit 1 at +âˆ, and such that for all `x : â„`, `a â†¦ condCDF Ï a x` is measurable. For all
`x : â„` and measurable set `s`, that function satisfies
`âˆ«â» a in s, ennreal.of_real (condCDF Ï a x) âˆ‚Ï.fst = Ï (s Ã—Ë¢ Iic x)`.

## Main definitions

* `ProbabilityTheory.condCDF Ï : Î± â†’ StieltjesFunction`: the conditional cdf of
  `Ï : Measure (Î± Ã— â„)`. A `StieltjesFunction` is a function `â„ â†’ â„` which is monotone and
  right-continuous.

## Main statements

* `ProbabilityTheory.set_lintegral_condCDF`: for all `a : Î±` and `x : â„`, all measurable set `s`,
  `âˆ«â» a in s, ENNReal.ofReal (condCDF Ï a x) âˆ‚Ï.fst = Ï (s Ã—Ë¢ Iic x)`.

## References

The construction of the conditional cdf in this file follows the proof of Theorem 3.4 in
[O. Kallenberg, Foundations of modern probability][kallenberg2021].

-/

open MeasureTheory Set Filter TopologicalSpace

open scoped NNReal ENNReal MeasureTheory Topology

namespace MeasureTheory.Measure

variable {Î± Î² : Type*} {mÎ± : MeasurableSpace Î±} (Ï : Measure (Î± Ã— â„))

/-- Measure on `Î±` such that for a measurable set `s`, `Ï.Iic_snd r s = Ï (s Ã—Ë¢ Iic r)`. -/
noncomputable def IicSnd (r : â„) : Measure Î± :=
  (Ï.restrict (univ Ã—Ë¢ Iic r)).fst
#align measure_theory.measure.Iic_snd MeasureTheory.Measure.IicSnd

theorem IicSnd_apply (r : â„) {s : Set Î±} (hs : MeasurableSet s) :
    Ï.IicSnd r s = Ï (s Ã—Ë¢ Iic r) := by
  rw [IicSnd, fst_apply hs,
    restrict_apply' (MeasurableSet.univ.prod (measurableSet_Iic : MeasurableSet (Iic r))), â†
    prod_univ, prod_inter_prod, inter_univ, univ_inter]
#align measure_theory.measure.Iic_snd_apply MeasureTheory.Measure.IicSnd_apply

theorem IicSnd_univ (r : â„) : Ï.IicSnd r univ = Ï (univ Ã—Ë¢ Iic r) :=
  IicSnd_apply Ï r MeasurableSet.univ
#align measure_theory.measure.Iic_snd_univ MeasureTheory.Measure.IicSnd_univ

theorem IicSnd_mono {r r' : â„} (h_le : r â‰¤ r') : Ï.IicSnd r â‰¤ Ï.IicSnd r' := by
  refine Measure.le_iff.2 fun s hs â†¦ ?_
  simp_rw [IicSnd_apply Ï _ hs]
  refine measure_mono (prod_subset_prod_iff.mpr (Or.inl âŸ¨subset_rfl, Iic_subset_Iic.mpr ?_âŸ©))
  exact mod_cast h_le
#align measure_theory.measure.Iic_snd_mono MeasureTheory.Measure.IicSnd_mono

theorem IicSnd_le_fst (r : â„) : Ï.IicSnd r â‰¤ Ï.fst := by
  refine Measure.le_iff.2 fun s hs â†¦ ?_
  simp_rw [fst_apply hs, IicSnd_apply Ï r hs]
  exact measure_mono (prod_subset_preimage_fst _ _)
#align measure_theory.measure.Iic_snd_le_fst MeasureTheory.Measure.IicSnd_le_fst

theorem IicSnd_ac_fst (r : â„) : Ï.IicSnd r â‰ª Ï.fst :=
  Measure.absolutelyContinuous_of_le (IicSnd_le_fst Ï r)
#align measure_theory.measure.Iic_snd_ac_fst MeasureTheory.Measure.IicSnd_ac_fst

theorem IsFiniteMeasure.IicSnd {Ï : Measure (Î± Ã— â„)} [IsFiniteMeasure Ï] (r : â„) :
    IsFiniteMeasure (Ï.IicSnd r) :=
  isFiniteMeasure_of_le _ (IicSnd_le_fst Ï _)
#align measure_theory.measure.is_finite_measure.Iic_snd MeasureTheory.Measure.IsFiniteMeasure.IicSnd

theorem iInf_IicSnd_gt (t : â„š) {s : Set Î±} (hs : MeasurableSet s) [IsFiniteMeasure Ï] :
    â¨… r : { r' : â„š // t < r' }, Ï.IicSnd r s = Ï.IicSnd t s := by
  simp_rw [Ï.IicSnd_apply _ hs, Measure.iInf_rat_gt_prod_Iic hs]
#align measure_theory.measure.infi_Iic_snd_gt MeasureTheory.Measure.iInf_IicSnd_gt

theorem tendsto_IicSnd_atTop {s : Set Î±} (hs : MeasurableSet s) :
    Tendsto (fun r : â„š â†¦ Ï.IicSnd r s) atTop (ğ“ (Ï.fst s)) := by
  simp_rw [Ï.IicSnd_apply _ hs, fst_apply hs, â† prod_univ]
  rw [â† Real.iUnion_Iic_rat, prod_iUnion]
  refine tendsto_measure_iUnion fun r q hr_le_q x â†¦ ?_
  simp only [mem_prod, mem_Iic, and_imp]
  refine fun hxs hxr â†¦ âŸ¨hxs, hxr.trans ?_âŸ©
  exact mod_cast hr_le_q
#align measure_theory.measure.tendsto_Iic_snd_at_top MeasureTheory.Measure.tendsto_IicSnd_atTop

theorem tendsto_IicSnd_atBot [IsFiniteMeasure Ï] {s : Set Î±} (hs : MeasurableSet s) :
    Tendsto (fun r : â„š â†¦ Ï.IicSnd r s) atBot (ğ“ 0) := by
  simp_rw [Ï.IicSnd_apply _ hs]
  have h_empty : Ï (s Ã—Ë¢ âˆ…) = 0 := by simp only [prod_empty, measure_empty]
  rw [â† h_empty, â† Real.iInter_Iic_rat, prod_iInter]
  suffices h_neg :
      Tendsto (fun r : â„š â†¦ Ï (s Ã—Ë¢ Iic â†‘(-r))) atTop (ğ“ (Ï (â‹‚ r : â„š, s Ã—Ë¢ Iic â†‘(-r)))) by
    have h_inter_eq : â‹‚ r : â„š, s Ã—Ë¢ Iic â†‘(-r) = â‹‚ r : â„š, s Ã—Ë¢ Iic (r : â„) := by
      ext1 x
      simp only [Rat.cast_eq_id, id.def, mem_iInter, mem_prod, mem_Iic]
      refine âŸ¨fun h i â†¦ âŸ¨(h i).1, ?_âŸ©, fun h i â†¦ âŸ¨(h i).1, ?_âŸ©âŸ© <;> have h' := h (-i)
      Â· rw [neg_neg] at h'; exact h'.2
      Â· exact h'.2
    rw [h_inter_eq] at h_neg
    have h_fun_eq : (fun r : â„š â†¦ Ï (s Ã—Ë¢ Iic (r : â„))) = fun r : â„š â†¦ Ï (s Ã—Ë¢ Iic â†‘(- -r)) := by
      simp_rw [neg_neg]
    rw [h_fun_eq]
    exact h_neg.comp tendsto_neg_atBot_atTop
  refine tendsto_measure_iInter (fun q â†¦ hs.prod measurableSet_Iic) ?_ âŸ¨0, measure_ne_top Ï _âŸ©
  refine fun q r hqr â†¦ prod_subset_prod_iff.mpr (Or.inl âŸ¨subset_rfl, fun x hx â†¦ ?_âŸ©)
  simp only [Rat.cast_neg, mem_Iic] at hx âŠ¢
  refine hx.trans (neg_le_neg ?_)
  exact mod_cast hqr
#align measure_theory.measure.tendsto_Iic_snd_at_bot MeasureTheory.Measure.tendsto_IicSnd_atBot

end MeasureTheory.Measure

open MeasureTheory

namespace ProbabilityTheory

variable {Î± Î² Î¹ : Type*} {mÎ± : MeasurableSpace Î±}

attribute [local instance] MeasureTheory.Measure.IsFiniteMeasure.IicSnd

/-! ### Auxiliary definitions

We build towards the definition of `ProbabilityTheory.cond_cdf`. We first define
`ProbabilityTheory.preCDF`, a function defined on `Î± Ã— â„š` with the properties of a cdf almost
everywhere.  -/

/-- `preCDF` is the Radon-Nikodym derivative of `Ï.IicSnd` with respect to `Ï.fst` at each
`r : â„š`. This function `â„š â†’ Î± â†’ â„â‰¥0âˆ` is such that for almost all `a : Î±`, the function `â„š â†’ â„â‰¥0âˆ`
satisfies the properties of a cdf (monotone with limit 0 at -âˆ and 1 at +âˆ, right-continuous).

We define this function on `â„š` and not `â„` because `â„š` is countable, which allows us to prove
properties of the form `âˆ€áµ a âˆ‚Ï.fst, âˆ€ q, P (preCDF q a)`, instead of the weaker
`âˆ€ q, âˆ€áµ a âˆ‚Ï.fst, P (preCDF q a)`. -/
noncomputable def preCDF (Ï : Measure (Î± Ã— â„)) (r : â„š) : Î± â†’ â„â‰¥0âˆ :=
  Measure.rnDeriv (Ï.IicSnd r) Ï.fst
#align probability_theory.pre_cdf ProbabilityTheory.preCDF

theorem measurable_preCDF {Ï : Measure (Î± Ã— â„)} {r : â„š} : Measurable (preCDF Ï r) :=
  Measure.measurable_rnDeriv _ _
#align probability_theory.measurable_pre_cdf ProbabilityTheory.measurable_preCDF

lemma measurable_preCDF' {Ï : Measure (Î± Ã— â„)} :
    Measurable fun a r â†¦ ENNReal.toReal (preCDF Ï r a) := by
    rw [measurable_pi_iff]
    exact fun _ â†¦ measurable_preCDF.ennreal_toReal

theorem withDensity_preCDF (Ï : Measure (Î± Ã— â„)) (r : â„š) [IsFiniteMeasure Ï] :
    Ï.fst.withDensity (preCDF Ï r) = Ï.IicSnd r :=
  Measure.absolutelyContinuous_iff_withDensity_rnDeriv_eq.mp (Measure.IicSnd_ac_fst Ï r)
#align probability_theory.with_density_pre_cdf ProbabilityTheory.withDensity_preCDF

theorem set_lintegral_preCDF_fst (Ï : Measure (Î± Ã— â„)) (r : â„š) {s : Set Î±} (hs : MeasurableSet s)
    [IsFiniteMeasure Ï] : âˆ«â» x in s, preCDF Ï r x âˆ‚Ï.fst = Ï.IicSnd r s := by
  have : âˆ€ r, âˆ«â» x in s, preCDF Ï r x âˆ‚Ï.fst = âˆ«â» x in s, (preCDF Ï r * 1) x âˆ‚Ï.fst := by
    simp only [mul_one, eq_self_iff_true, forall_const]
  rw [this, â† set_lintegral_withDensity_eq_set_lintegral_mul _ measurable_preCDF _ hs]
  Â· simp only [withDensity_preCDF Ï r, Pi.one_apply, lintegral_one, Measure.restrict_apply,
      MeasurableSet.univ, univ_inter]
  Â· rw [(_ : (1 : Î± â†’ â„â‰¥0âˆ) = fun _ â†¦ 1)]
    exacts [measurable_const, rfl]
#align probability_theory.set_lintegral_pre_cdf_fst ProbabilityTheory.set_lintegral_preCDF_fst

theorem monotone_preCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] :
    âˆ€áµ a âˆ‚Ï.fst, Monotone fun r â†¦ preCDF Ï r a := by
  simp_rw [Monotone, ae_all_iff]
  refine fun r r' hrr' â†¦ ae_le_of_forall_set_lintegral_le_of_sigmaFinite measurable_preCDF
    measurable_preCDF fun s hs _ â†¦ ?_
  rw [set_lintegral_preCDF_fst Ï r hs, set_lintegral_preCDF_fst Ï r' hs]
  exact Measure.IicSnd_mono Ï (mod_cast hrr') s
#align probability_theory.monotone_pre_cdf ProbabilityTheory.monotone_preCDF

theorem set_lintegral_iInf_gt_preCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (t : â„š) {s : Set Î±}
    (hs : MeasurableSet s) : âˆ«â» x in s, â¨… r : Ioi t, preCDF Ï r x âˆ‚Ï.fst = Ï.IicSnd t s := by
  refine le_antisymm ?_ ?_
  Â· have h : âˆ€ q : Ioi t, âˆ«â» x in s, â¨… r : Ioi t, preCDF Ï r x âˆ‚Ï.fst â‰¤ Ï.IicSnd q s := by
      intro q
      rw [â† set_lintegral_preCDF_fst Ï _ hs]
      refine set_lintegral_mono_ae ?_ measurable_preCDF ?_
      Â· exact measurable_iInf fun _ â†¦ measurable_preCDF
      Â· filter_upwards [monotone_preCDF _] with a _
        exact fun _ â†¦ iInf_le _ q
    calc
      âˆ«â» x in s, â¨… r : Ioi t, preCDF Ï r x âˆ‚Ï.fst â‰¤ â¨… q : Ioi t, Ï.IicSnd q s := le_iInf h
      _ = Ï.IicSnd t s := Measure.iInf_IicSnd_gt Ï t hs
  Â· rw [(set_lintegral_preCDF_fst Ï t hs).symm]
    refine set_lintegral_mono_ae measurable_preCDF ?_ ?_
    Â· exact measurable_iInf fun _ â†¦ measurable_preCDF
    Â· filter_upwards [monotone_preCDF _] with a ha_mono
      exact fun _ â†¦ le_iInf fun r â†¦ ha_mono (le_of_lt r.prop)
#align probability_theory.set_lintegral_infi_gt_pre_cdf ProbabilityTheory.set_lintegral_iInf_gt_preCDF

theorem preCDF_le_one (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] :
    âˆ€áµ a âˆ‚Ï.fst, âˆ€ r, preCDF Ï r a â‰¤ 1 := by
  rw [ae_all_iff]
  refine fun r â†¦ ae_le_of_forall_set_lintegral_le_of_sigmaFinite measurable_preCDF
    measurable_const fun s hs _ â†¦ ?_
  rw [set_lintegral_preCDF_fst Ï r hs]
  simp only [Pi.one_apply, lintegral_one, Measure.restrict_apply, MeasurableSet.univ, univ_inter]
  exact Measure.IicSnd_le_fst Ï r s
#align probability_theory.pre_cdf_le_one ProbabilityTheory.preCDF_le_one

theorem set_integral_preCDF_fst (Ï : Measure (Î± Ã— â„)) (r : â„š) {s : Set Î±} (hs : MeasurableSet s)
    [IsFiniteMeasure Ï] :
    âˆ« x in s, (preCDF Ï r x).toReal âˆ‚Ï.fst = (Ï.IicSnd r s).toReal := by
  rw [integral_toReal]
  Â· rw [set_lintegral_preCDF_fst _ _ hs]
  Â· exact measurable_preCDF.aemeasurable
  Â· refine ae_restrict_of_ae ?_
    filter_upwards [preCDF_le_one Ï] with a ha
    exact (ha r).trans_lt ENNReal.one_lt_top

theorem tendsto_lintegral_preCDF_atTop (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] :
    Tendsto (fun r â†¦ âˆ«â» a, preCDF Ï r a âˆ‚Ï.fst) atTop (ğ“ (Ï univ)) := by
  convert Ï.tendsto_IicSnd_atTop MeasurableSet.univ
  Â· rw [â† set_lintegral_univ, set_lintegral_preCDF_fst Ï _ MeasurableSet.univ]
  Â· exact Measure.fst_univ.symm
#align probability_theory.tendsto_lintegral_pre_cdf_at_top ProbabilityTheory.tendsto_lintegral_preCDF_atTop

theorem tendsto_lintegral_preCDF_atBot (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] :
    Tendsto (fun r â†¦ âˆ«â» a, preCDF Ï r a âˆ‚Ï.fst) atBot (ğ“ 0) := by
  convert Ï.tendsto_IicSnd_atBot MeasurableSet.univ
  rw [â† set_lintegral_univ, set_lintegral_preCDF_fst Ï _ MeasurableSet.univ]
#align probability_theory.tendsto_lintegral_pre_cdf_at_bot ProbabilityTheory.tendsto_lintegral_preCDF_atBot

theorem tendsto_preCDF_atTop_one (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] :
    âˆ€áµ a âˆ‚Ï.fst, Tendsto (fun r â†¦ preCDF Ï r a) atTop (ğ“ 1) := by
  -- We show first that `preCDF` has a limit almost everywhere. That limit has to be at most 1.
  -- We then show that the integral of `preCDF` tends to the integral of 1, and that it also tends
  -- to the integral of the limit. Since the limit is at most 1 and has same integral as 1, it is
  -- equal to 1 a.e.
  have h_mono := monotone_preCDF Ï
  have h_le_one := preCDF_le_one Ï
  -- `preCDF` has a limit a.e.
  have h_exists : âˆ€áµ a âˆ‚Ï.fst, âˆƒ l, Tendsto (fun r â†¦ preCDF Ï r a) atTop (ğ“ l) := by
    filter_upwards [h_mono] with a ha_mono
    exact âŸ¨_, tendsto_atTop_iSup ha_monoâŸ©
  classical
  -- let `F` be the pointwise limit of `preCDF` where it exists, and 0 elsewhere.
  let F : Î± â†’ â„â‰¥0âˆ := fun a â†¦
    if h : âˆƒ l, Tendsto (fun r â†¦ preCDF Ï r a) atTop (ğ“ l) then h.choose else 0
  have h_tendsto_â„š : âˆ€áµ a âˆ‚Ï.fst, Tendsto (fun r â†¦ preCDF Ï r a) atTop (ğ“ (F a)) := by
    filter_upwards [h_exists] with a ha
    simp_rw [dif_pos ha]
    exact ha.choose_spec
  have h_tendsto_â„• : âˆ€áµ a âˆ‚Ï.fst, Tendsto (fun n : â„• â†¦ preCDF Ï n a) atTop (ğ“ (F a)) := by
    filter_upwards [h_tendsto_â„š] with a ha using ha.comp tendsto_nat_cast_atTop_atTop
  have hF_ae_meas : AEMeasurable F Ï.fst := by
    refine aemeasurable_of_tendsto_metrizable_ae _ (fun n â†¦ ?_) h_tendsto_â„š
    exact measurable_preCDF.aemeasurable
  have hF_le_one : âˆ€áµ a âˆ‚Ï.fst, F a â‰¤ 1 := by
    filter_upwards [h_tendsto_â„š, h_le_one] with a ha ha_le using le_of_tendsto' ha ha_le
  -- it suffices to show that the limit `F` is 1 a.e.
  suffices âˆ€áµ a âˆ‚Ï.fst, F a = 1 by
    filter_upwards [h_tendsto_â„š, this] with a ha_tendsto ha_eq
    rwa [ha_eq] at ha_tendsto
  -- since `F` is at most 1, proving that its integral is the same as the integral of 1 will tell
  -- us that `F` is 1 a.e.
  have h_lintegral_eq : âˆ«â» a, F a âˆ‚Ï.fst = âˆ«â» _, 1 âˆ‚Ï.fst := by
    have h_lintegral :
        Tendsto (fun r : â„• â†¦ âˆ«â» a, preCDF Ï r a âˆ‚Ï.fst) atTop (ğ“ (âˆ«â» a, F a âˆ‚Ï.fst)) := by
      refine lintegral_tendsto_of_tendsto_of_monotone
        (fun _ â†¦ measurable_preCDF.aemeasurable) ?_ h_tendsto_â„•
      filter_upwards [h_mono] with a ha
      refine fun n m hnm â†¦ ha ?_
      exact mod_cast hnm
    have h_lintegral' :
        Tendsto (fun r : â„• â†¦ âˆ«â» a, preCDF Ï r a âˆ‚Ï.fst) atTop (ğ“ (âˆ«â» _, 1 âˆ‚Ï.fst)) := by
      rw [lintegral_one, Measure.fst_univ]
      exact (tendsto_lintegral_preCDF_atTop Ï).comp tendsto_nat_cast_atTop_atTop
    exact tendsto_nhds_unique h_lintegral h_lintegral'
  have : âˆ«â» a, 1 - F a âˆ‚Ï.fst = 0 := by
    rw [lintegral_sub' hF_ae_meas _ hF_le_one, h_lintegral_eq, tsub_self]
    calc
      âˆ«â» a, F a âˆ‚Ï.fst = âˆ«â» _, 1 âˆ‚Ï.fst := h_lintegral_eq
      _ = Ï.fst univ := lintegral_one
      _ = Ï univ := Measure.fst_univ
      _ â‰  âˆ := measure_ne_top Ï _
  rw [lintegral_eq_zero_iff' (aemeasurable_const.sub hF_ae_meas)] at this
  filter_upwards [this, hF_le_one] with ha h_one_sub_eq_zero h_le_one
  rw [Pi.zero_apply, tsub_eq_zero_iff_le] at h_one_sub_eq_zero
  exact le_antisymm h_le_one h_one_sub_eq_zero
#align probability_theory.tendsto_pre_cdf_at_top_one ProbabilityTheory.tendsto_preCDF_atTop_one

theorem tendsto_preCDF_atBot_zero (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] :
    âˆ€áµ a âˆ‚Ï.fst, Tendsto (fun r â†¦ preCDF Ï r a) atBot (ğ“ 0) := by
  -- We show first that `preCDF` has a limit in â„â‰¥0âˆ almost everywhere.
  -- We then show that the integral of `preCDF` tends to 0, and that it also tends
  -- to the integral of the limit. Since the limit has integral 0, it is equal to 0 a.e.
  suffices âˆ€áµ a âˆ‚Ï.fst, Tendsto (fun r â†¦ preCDF Ï (-r) a) atTop (ğ“ 0) by
    filter_upwards [this] with a ha
    have h_eq_neg : (fun r : â„š â†¦ preCDF Ï r a) = fun r : â„š â†¦ preCDF Ï (- -r) a := by
      simp_rw [neg_neg]
    rw [h_eq_neg]
    exact ha.comp tendsto_neg_atBot_atTop
  have h_exists : âˆ€áµ a âˆ‚Ï.fst, âˆƒ l, Tendsto (fun r â†¦ preCDF Ï (-r) a) atTop (ğ“ l) := by
    filter_upwards [monotone_preCDF Ï] with a ha
    have h_anti : Antitone fun r â†¦ preCDF Ï (-r) a := fun p q hpq â†¦ ha (neg_le_neg hpq)
    exact âŸ¨_, tendsto_atTop_iInf h_antiâŸ©
  classical
  let F : Î± â†’ â„â‰¥0âˆ := fun a â†¦
    if h : âˆƒ l, Tendsto (fun r â†¦ preCDF Ï (-r) a) atTop (ğ“ l) then h.choose else 0
  have h_tendsto : âˆ€áµ a âˆ‚Ï.fst, Tendsto (fun r â†¦ preCDF Ï (-r) a) atTop (ğ“ (F a)) := by
    filter_upwards [h_exists] with a ha
    simp_rw [dif_pos ha]
    exact ha.choose_spec
  suffices h_lintegral_eq : âˆ«â» a, F a âˆ‚Ï.fst = 0 by
    have hF_ae_meas : AEMeasurable F Ï.fst := by
      refine aemeasurable_of_tendsto_metrizable_ae _ (fun n â†¦ ?_) h_tendsto
      exact measurable_preCDF.aemeasurable
    rw [lintegral_eq_zero_iff' hF_ae_meas] at h_lintegral_eq
    filter_upwards [h_tendsto, h_lintegral_eq] with a ha_tendsto ha_eq
    rwa [ha_eq] at ha_tendsto
  have h_lintegral :
      Tendsto (fun r â†¦ âˆ«â» a, preCDF Ï (-r) a âˆ‚Ï.fst) atTop (ğ“ (âˆ«â» a, F a âˆ‚Ï.fst)) := by
    refine tendsto_lintegral_filter_of_dominated_convergence (fun _ â†¦ 1)
      (eventually_of_forall fun _ â†¦ measurable_preCDF) (eventually_of_forall fun _ â†¦ ?_) ?_
      h_tendsto
    Â· filter_upwards [preCDF_le_one Ï] with a ha using ha _
    Â· rw [lintegral_one]
      exact measure_ne_top _ _
  have h_lintegral' : Tendsto (fun r â†¦ âˆ«â» a, preCDF Ï (-r) a âˆ‚Ï.fst) atTop (ğ“ 0) := by
    have h_lintegral_eq : (fun r â†¦ âˆ«â» a, preCDF Ï (-r) a âˆ‚Ï.fst)
        = fun r : â„š â†¦ Ï (univ Ã—Ë¢ Iic (-r : â„)) := by
      ext1 n
      rw [â† set_lintegral_univ, set_lintegral_preCDF_fst Ï _ MeasurableSet.univ,
        Measure.IicSnd_univ]
      norm_cast
    rw [h_lintegral_eq]
    have h_zero_eq_measure_iInter : (0 : â„â‰¥0âˆ) = Ï (â‹‚ r : â„š, univ Ã—Ë¢ Iic (-r : â„)) := by
      suffices â‹‚ r : â„š, Iic (-(r : â„)) = âˆ… by rw [â† prod_iInter, this, prod_empty, measure_empty]
      ext1 x
      simp only [mem_iInter, mem_Iic, mem_empty_iff_false, iff_false_iff, not_forall, not_le]
      simp_rw [neg_lt]
      exact exists_rat_gt _
    rw [h_zero_eq_measure_iInter]
    refine tendsto_measure_iInter (fun n â†¦ MeasurableSet.univ.prod measurableSet_Iic)
        (fun i j hij x â†¦ ?_) âŸ¨0, measure_ne_top Ï _âŸ©
    simp only [mem_prod, mem_univ, mem_Iic, true_and_iff]
    refine fun hxj â†¦ hxj.trans (neg_le_neg ?_)
    exact mod_cast hij
  exact tendsto_nhds_unique h_lintegral h_lintegral'
#align probability_theory.tendsto_pre_cdf_at_bot_zero ProbabilityTheory.tendsto_preCDF_atBot_zero

theorem inf_gt_preCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] :
    âˆ€áµ a âˆ‚Ï.fst, âˆ€ t : â„š, â¨… r : Ioi t, preCDF Ï r a = preCDF Ï t a := by
  rw [ae_all_iff]
  refine fun t â†¦ ae_eq_of_forall_set_lintegral_eq_of_sigmaFinite ?_ measurable_preCDF ?_
  Â· exact measurable_iInf fun i â†¦ measurable_preCDF
  intro s hs _
  rw [set_lintegral_iInf_gt_preCDF Ï t hs, set_lintegral_preCDF_fst Ï t hs]
#align probability_theory.inf_gt_pre_cdf ProbabilityTheory.inf_gt_preCDF

#noalign probability_theory.has_cond_cdf
#noalign probability_theory.has_cond_cdf_ae
#noalign probability_theory.cond_cdf_set
#noalign probability_theory.measurable_set_cond_cdf_set
#noalign probability_theory.has_cond_cdf_of_mem_cond_cdf_set
#noalign probability_theory.mem_cond_cdf_set_ae
#noalign probability_theory.cond_cdf_rat
#noalign probability_theory.cond_cdf_rat_of_not_mem
#noalign probability_theory.cond_cdf_rat_of_mem
#noalign probability_theory.monotone_cond_cdf_rat
#noalign probability_theory.measurable_cond_cdf_rat
#noalign probability_theory.cond_cdf_rat_nonneg
#noalign probability_theory.cond_cdf_rat_le_one
#noalign probability_theory.tendsto_cond_cdf_rat_at_bot
#noalign probability_theory.tendsto_cond_cdf_rat_at_top
#noalign probability_theory.cond_cdf_rat_ae_eq
#noalign probability_theory.of_real_cond_cdf_rat_ae_eq
#noalign probability_theory.inf_gt_cond_cdf_rat
#noalign probability_theory.cond_cdf'
#noalign probability_theory.cond_cdf'_def
#noalign probability_theory.cond_cdf'_eq_cond_cdf_rat
#noalign probability_theory.cond_cdf'_nonneg
#noalign probability_theory.bdd_below_range_cond_cdf_rat_gt
#noalign probability_theory.monotone_cond_cdf'
#noalign probability_theory.continuous_within_at_cond_cdf'_Ici

/-! ### Conditional cdf -/

lemma isRatStieltjesPoint_ae (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] :
    âˆ€áµ a âˆ‚Ï.fst, IsRatStieltjesPoint (fun a r â†¦ (preCDF Ï r a).toReal) a := by
  filter_upwards [monotone_preCDF Ï, preCDF_le_one Ï, tendsto_preCDF_atTop_one Ï,
    tendsto_preCDF_atBot_zero Ï, inf_gt_preCDF Ï] with a h1 h2 h3 h4 h5
  constructor
  Â· intro r r' hrr'
    have h_ne_top : âˆ€ r, preCDF Ï r a â‰  âˆ := fun r â†¦ ((h2 r).trans_lt ENNReal.one_lt_top).ne
    rw [ENNReal.toReal_le_toReal (h_ne_top _) (h_ne_top _)]
    exact h1 hrr'
  Â· rw [â† ENNReal.one_toReal, ENNReal.tendsto_toReal_iff]
    Â· exact h3
    Â· exact fun r â†¦ ((h2 r).trans_lt ENNReal.one_lt_top).ne
    Â· exact ENNReal.one_ne_top
  Â· rw [â† ENNReal.zero_toReal, ENNReal.tendsto_toReal_iff]
    Â· exact h4
    Â· exact fun r â†¦ ((h2 r).trans_lt ENNReal.one_lt_top).ne
    Â· exact ENNReal.zero_ne_top
  Â· intro q
    rw [â† ENNReal.toReal_iInf]
    Â· suffices â¨… i : â†¥(Ioi q), preCDF Ï (â†‘i) a = preCDF Ï q a by rw [this]
      rw [â† h5]
    Â· exact fun r â†¦ ((h2 r).trans_lt ENNReal.one_lt_top).ne

theorem integrable_preCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (x : â„š) :
    Integrable (fun a â†¦ (preCDF Ï x a).toReal) Ï.fst := by
  refine integrable_of_forall_fin_meas_le _ (measure_lt_top Ï.fst univ) ?_ fun t _ _ â†¦ ?_
  Â· exact  measurable_preCDF.ennreal_toReal.aestronglyMeasurable
  Â· simp_rw [â† ofReal_norm_eq_coe_nnnorm, Real.norm_of_nonneg ENNReal.toReal_nonneg]
    rw [â† lintegral_one]
    refine (set_lintegral_le_lintegral _ _).trans (lintegral_mono_ae ?_)
    filter_upwards [preCDF_le_one Ï] with a ha using ENNReal.ofReal_toReal_le.trans (ha _)

lemma isRatCondKernelCDF_preCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] :
    IsRatCondKernelCDF (fun p r â†¦ (preCDF Ï r p.2).toReal)
      (kernel.const Unit Ï) (kernel.const Unit Ï.fst) where
  measurable := measurable_preCDF'.comp measurable_snd
  isRatStieltjesPoint_ae a := by
    filter_upwards [isRatStieltjesPoint_ae Ï] with a ha
    exact âŸ¨ha.mono, ha.tendsto_atTop_one, ha.tendsto_atBot_zero, ha.iInf_rat_gt_eqâŸ©
  integrable _ q := integrable_preCDF Ï q
  set_integral a s hs q := by rw [kernel.const_apply, kernel.const_apply, set_integral_preCDF_fst _ _ hs,
    Measure.IicSnd_apply _ _ hs]

/-- Conditional cdf of the measure given the value on `Î±`, as a Stieltjes function. -/
noncomputable def condCDF (Ï : Measure (Î± Ã— â„)) (a : Î±) : StieltjesFunction :=
  stieltjesOfMeasurableRat (fun a r â†¦ (preCDF Ï r a).toReal) measurable_preCDF' a
#align probability_theory.cond_cdf ProbabilityTheory.condCDF

lemma condCDF_eq_stieltjesOfMeasurableRat_unit_prod (Ï : Measure (Î± Ã— â„)) (a : Î±) :
    condCDF Ï a = stieltjesOfMeasurableRat (fun (p : Unit Ã— Î±) r â†¦ (preCDF Ï r p.2).toReal)
      (measurable_preCDF'.comp measurable_snd) ((), a) := by
  ext x
  rw [condCDF, â† stieltjesOfMeasurableRat_unit_prod]

#noalign probability_theory.cond_cdf_eq_cond_cdf_rat

/-- The conditional cdf is non-negative for all `a : Î±`. -/
theorem condCDF_nonneg (Ï : Measure (Î± Ã— â„)) (a : Î±) (r : â„) : 0 â‰¤ condCDF Ï a r :=
  stieltjesOfMeasurableRat_nonneg _ a r
#align probability_theory.cond_cdf_nonneg ProbabilityTheory.condCDF_nonneg

/-- The conditional cdf is lower or equal to 1 for all `a : Î±`. -/
theorem condCDF_le_one (Ï : Measure (Î± Ã— â„)) (a : Î±) (x : â„) : condCDF Ï a x â‰¤ 1 :=
  stieltjesOfMeasurableRat_le_one _ _ _
#align probability_theory.cond_cdf_le_one ProbabilityTheory.condCDF_le_one

/-- The conditional cdf tends to 0 at -âˆ for all `a : Î±`. -/
theorem tendsto_condCDF_atBot (Ï : Measure (Î± Ã— â„)) (a : Î±) :
    Tendsto (condCDF Ï a) atBot (ğ“ 0) := tendsto_stieltjesOfMeasurableRat_atBot _ _
#align probability_theory.tendsto_cond_cdf_at_bot ProbabilityTheory.tendsto_condCDF_atBot

/-- The conditional cdf tends to 1 at +âˆ for all `a : Î±`. -/
theorem tendsto_condCDF_atTop (Ï : Measure (Î± Ã— â„)) (a : Î±) :
    Tendsto (condCDF Ï a) atTop (ğ“ 1) := tendsto_stieltjesOfMeasurableRat_atTop _ _
#align probability_theory.tendsto_cond_cdf_at_top ProbabilityTheory.tendsto_condCDF_atTop

theorem condCDF_ae_eq (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (r : â„š) :
    (fun a â†¦ condCDF Ï a r) =áµ[Ï.fst] fun a â†¦ (preCDF Ï r a).toReal := by
  filter_upwards [isRatStieltjesPoint_ae Ï] with a ha
  rw [condCDF, stieltjesOfMeasurableRat_eq, toRatCDF_of_isRatStieltjesPoint ha]
#align probability_theory.cond_cdf_ae_eq ProbabilityTheory.condCDF_ae_eq

theorem ofReal_condCDF_ae_eq (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (r : â„š) :
    (fun a â†¦ ENNReal.ofReal (condCDF Ï a r)) =áµ[Ï.fst] preCDF Ï r := by
  filter_upwards [condCDF_ae_eq Ï r, preCDF_le_one Ï] with a ha ha_le_one
  rw [ha, ENNReal.ofReal_toReal]
  exact ((ha_le_one r).trans_lt ENNReal.one_lt_top).ne
#align probability_theory.of_real_cond_cdf_ae_eq ProbabilityTheory.ofReal_condCDF_ae_eq

/-- The conditional cdf is a measurable function of `a : Î±` for all `x : â„`. -/
theorem measurable_condCDF (Ï : Measure (Î± Ã— â„)) (x : â„) : Measurable fun a â†¦ condCDF Ï a x :=
  measurable_stieltjesOfMeasurableRat _ _
#align probability_theory.measurable_cond_cdf ProbabilityTheory.measurable_condCDF

#noalign probability_theory.set_lintegral_cond_cdf_rat

theorem set_lintegral_condCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (x : â„) {s : Set Î±}
    (hs : MeasurableSet s) :
    âˆ«â» a in s, ENNReal.ofReal (condCDF Ï a x) âˆ‚Ï.fst = Ï (s Ã—Ë¢ Iic x) := by
  have h := set_lintegral_stieltjesOfMeasurableRat (isRatCondKernelCDF_preCDF Ï) () x hs
  simp only [kernel.const_apply] at h
  rw [â† h]
  congr with a
  congr
  exact condCDF_eq_stieltjesOfMeasurableRat_unit_prod _ _
#align probability_theory.set_lintegral_cond_cdf ProbabilityTheory.set_lintegral_condCDF

theorem lintegral_condCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (x : â„) :
    âˆ«â» a, ENNReal.ofReal (condCDF Ï a x) âˆ‚Ï.fst = Ï (univ Ã—Ë¢ Iic x) := by
  rw [â† set_lintegral_univ, set_lintegral_condCDF Ï _ MeasurableSet.univ]
#align probability_theory.lintegral_cond_cdf ProbabilityTheory.lintegral_condCDF

/-- The conditional cdf is a strongly measurable function of `a : Î±` for all `x : â„`. -/
theorem stronglyMeasurable_condCDF (Ï : Measure (Î± Ã— â„)) (x : â„) :
    StronglyMeasurable fun a â†¦ condCDF Ï a x := stronglyMeasurable_stieltjesOfMeasurableRat _ _
#align probability_theory.strongly_measurable_cond_cdf ProbabilityTheory.stronglyMeasurable_condCDF

theorem integrable_condCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (x : â„) :
    Integrable (fun a â†¦ condCDF Ï a x) Ï.fst := by
  refine integrable_of_forall_fin_meas_le _ (measure_lt_top Ï.fst univ) ?_ fun t _ _ â†¦ ?_
  Â· exact (stronglyMeasurable_condCDF Ï _).aestronglyMeasurable
  Â· have : âˆ€ y, (â€–condCDF Ï y xâ€–â‚Š : â„â‰¥0âˆ) â‰¤ 1 := by
      intro y
      rw [Real.nnnorm_of_nonneg (condCDF_nonneg _ _ _)]
      -- Porting note: was exact_mod_cast condCDF_le_one _ _ _
      simp only [ENNReal.coe_le_one_iff]
      exact condCDF_le_one _ _ _
    refine
      (set_lintegral_mono (measurable_condCDF _ _).ennnorm measurable_one fun y _ â†¦ this y).trans
        ?_
    simp only [Pi.one_apply, lintegral_one, Measure.restrict_apply, MeasurableSet.univ, univ_inter]
    exact measure_mono (subset_univ _)
#align probability_theory.integrable_cond_cdf ProbabilityTheory.integrable_condCDF

theorem set_integral_condCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (x : â„) {s : Set Î±}
    (hs : MeasurableSet s) : âˆ« a in s, condCDF Ï a x âˆ‚Ï.fst = (Ï (s Ã—Ë¢ Iic x)).toReal := by
  have h := set_lintegral_condCDF Ï x hs
  rw [â† ofReal_integral_eq_lintegral_ofReal] at h
  Â· rw [â† h, ENNReal.toReal_ofReal]
    exact integral_nonneg fun _ â†¦ condCDF_nonneg _ _ _
  Â· exact (integrable_condCDF _ _).integrableOn
  Â· exact eventually_of_forall fun _ â†¦ condCDF_nonneg _ _ _
#align probability_theory.set_integral_cond_cdf ProbabilityTheory.set_integral_condCDF

theorem integral_condCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (x : â„) :
    âˆ« a, condCDF Ï a x âˆ‚Ï.fst = (Ï (univ Ã—Ë¢ Iic x)).toReal := by
  rw [â† set_integral_condCDF Ï _ MeasurableSet.univ, Measure.restrict_univ]
#align probability_theory.integral_cond_cdf ProbabilityTheory.integral_condCDF

lemma isCondKernelCDF_condCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] :
    IsCondKernelCDF (fun p : Unit Ã— Î± â†¦ condCDF Ï p.2) (kernel.const Unit Ï)
      (kernel.const Unit Ï.fst) where
  measurable x := (measurable_condCDF Ï x).comp measurable_snd
  integrable _ x := integrable_condCDF Ï x
  tendsto_atTop_one p := tendsto_condCDF_atTop Ï p.2
  tendsto_atBot_zero p := tendsto_condCDF_atBot Ï p.2
  set_integral _ _ hs x := set_integral_condCDF Ï x hs

#noalign probability_theory.measure_cond_cdf_Iic
#noalign probability_theory.measure_cond_cdf_univ
#noalign probability_theory.measurable_measure_cond_cdf

end ProbabilityTheory
