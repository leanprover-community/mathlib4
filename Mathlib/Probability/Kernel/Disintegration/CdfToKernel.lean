/-
Copyright (c) 2024 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Function.AEEqOfIntegral
import Mathlib.Probability.Kernel.Composition
import Mathlib.Probability.Kernel.Disintegration.MeasurableStieltjes

/-!
# Building a Markov kernel from a conditional cumulative distribution function

Let `Îº : kernel Î± (Î² Ã— â„)` and `Î½ : kernel Î± Î²` be two finite kernels.
A function `f : Î± Ã— Î² â†’ StieltjesFunction` is called a conditional kernel CDF of `Îº` with respect
to `Î½` if it is measurable, tends to to 0 at -âˆ and to 1 at +âˆ for all `p : Î± Ã— Î²`,
`fun b â†¦ f (a, b) x` is `(Î½ a)`-integrable for all `a : Î±` and `x : â„` and for all measurable
sets `s : Set Î²`, `âˆ« b in s, f (a, b) x âˆ‚(Î½ a) = (Îº a (s Ã—Ë¢ Iic x)).toReal`.

From such a function with property `hf : IsCondKernelCDF f Îº Î½`, we can build a `kernel (Î± Ã— Î²) â„`
denoted by `hf.toKernel f` such that `Îº = Î½ âŠ—â‚– hf.toKernel f`.

## Main definitions

Let `Îº : kernel Î± (Î² Ã— â„)` and `Î½ : kernel Î± Î²`.

* `ProbabilityTheory.IsCondKernelCDF`: a function `f : Î± Ã— Î² â†’ StieltjesFunction` is called
  a conditional kernel CDF of `Îº` with respect to `Î½` if it is measurable, tends to to 0 at -âˆ and
  to 1 at +âˆ for all `p : Î± Ã— Î²`, if `fun b â†¦ f (a, b) x` is `(Î½ a)`-integrable for all `a : Î±` and
  `x : â„` and for all measurable sets `s : Set Î²`,
  `âˆ« b in s, f (a, b) x âˆ‚(Î½ a) = (Îº a (s Ã—Ë¢ Iic x)).toReal`.
* `ProbabilityTheory.IsCondKernelCDF.toKernel`: from a function `f : Î± Ã— Î² â†’ StieltjesFunction`
  with the property `hf : IsCondKernelCDF f Îº Î½`, build a `kernel (Î± Ã— Î²) â„` such that
  `Îº = Î½ âŠ—â‚– hf.toKernel f`.
* `ProbabilityTheory.IsRatCondKernelCDF`: a function `f : Î± Ã— Î² â†’ â„š â†’ â„` is called a rational
  conditional kernel CDF of `Îº` with respect to `Î½` if is measurable and satisfies the same
  integral conditions as in the definition of `IsCondKernelCDF`, and the `â„š â†’ â„` function `f (a, b)`
  satisfies the properties of a Sieltjes function for `(Î½ a)`-almost all `b : Î²`.

## Main statements

* `ProbabilityTheory.isCondKernelCDF_stieltjesOfMeasurableRat`: if `f : Î± Ã— Î² â†’ â„š â†’ â„` has the
  property `IsRatCondKernelCDF`, then `stieltjesOfMeasurableRat f` is a function
  `Î± Ã— Î² â†’ StieltjesFunction` with the property `IsCondKernelCDF`.
* `ProbabilityTheory.compProd_toKernel`: for `hf : IsCondKernelCDF f Îº Î½`, `Î½ âŠ—â‚– hf.toKernel f = Îº`.

-/

open MeasureTheory Set Filter TopologicalSpace

open scoped NNReal ENNReal MeasureTheory Topology ProbabilityTheory

namespace ProbabilityTheory

variable {Î± Î² : Type*} {mÎ± : MeasurableSpace Î±} {mÎ² : MeasurableSpace Î²}
  {Îº : kernel Î± (Î² Ã— â„)} {Î½ : kernel Î± Î²}

section stieltjesOfMeasurableRat

variable {f : Î± Ã— Î² â†’ â„š â†’ â„}

/-- a function `f : Î± Ã— Î² â†’ â„š â†’ â„` is called a rational conditional kernel CDF of `Îº` with respect
to `Î½` if is measurable, if `fun b â†¦ f (a, b) x` is `(Î½ a)`-integrable for all `a : Î±` and `x : â„`
and for all measurable sets `s : Set Î²`, `âˆ« b in s, f (a, b) x âˆ‚(Î½ a) = (Îº a (s Ã—Ë¢ Iic x)).toReal`.
Also the `â„š â†’ â„` function `f (a, b)` should satisfy the properties of a Sieltjes function for
`(Î½ a)`-almost all `b : Î²`. -/
structure IsRatCondKernelCDF (f : Î± Ã— Î² â†’ â„š â†’ â„) (Îº : kernel Î± (Î² Ã— â„)) (Î½ : kernel Î± Î²) : Prop :=
  (measurable : Measurable f)
  (isRatStieltjesPoint_ae (a : Î±) : âˆ€áµ b âˆ‚(Î½ a), IsRatStieltjesPoint f (a, b))
  (integrable (a : Î±) (q : â„š) : Integrable (fun b â†¦ f (a, b) q) (Î½ a))
  (set_integral (a : Î±) {s : Set Î²} (_hs : MeasurableSet s) (q : â„š) :
    âˆ« b in s, f (a, b) q âˆ‚(Î½ a) = (Îº a (s Ã—Ë¢ Iic (q : â„))).toReal)

lemma IsRatCondKernelCDF.mono (hf : IsRatCondKernelCDF f Îº Î½) (a : Î±) :
    âˆ€áµ b âˆ‚(Î½ a), Monotone (f (a, b)) := by
  filter_upwards [hf.isRatStieltjesPoint_ae a] with b hb using hb.mono

lemma IsRatCondKernelCDF.tendsto_atTop_one (hf : IsRatCondKernelCDF f Îº Î½) (a : Î±) :
    âˆ€áµ b âˆ‚(Î½ a), Tendsto (f (a, b)) atTop (ğ“ 1) := by
  filter_upwards [hf.isRatStieltjesPoint_ae a] with b hb using hb.tendsto_atTop_one

lemma IsRatCondKernelCDF.tendsto_atBot_zero (hf : IsRatCondKernelCDF f Îº Î½) (a : Î±) :
    âˆ€áµ b âˆ‚(Î½ a), Tendsto (f (a, b)) atBot (ğ“ 0) := by
  filter_upwards [hf.isRatStieltjesPoint_ae a] with b hb using hb.tendsto_atBot_zero

lemma IsRatCondKernelCDF.iInf_rat_gt_eq (hf : IsRatCondKernelCDF f Îº Î½) (a : Î±) :
    âˆ€áµ b âˆ‚(Î½ a), âˆ€ q, â¨… r : Ioi q, f (a, b) r = f (a, b) q := by
  filter_upwards [hf.isRatStieltjesPoint_ae a] with b hb using hb.iInf_rat_gt_eq

lemma stieltjesOfMeasurableRat_ae_eq (hf : IsRatCondKernelCDF f Îº Î½) (a : Î±) (q : â„š) :
    (fun b â†¦ stieltjesOfMeasurableRat f hf.measurable (a, b) q) =áµ[Î½ a] fun b â†¦ f (a, b) q := by
  filter_upwards [hf.isRatStieltjesPoint_ae a] with a ha
  rw [stieltjesOfMeasurableRat_eq, toRatCDF_of_isRatStieltjesPoint ha]

lemma set_integral_stieltjesOfMeasurableRat_rat (hf : IsRatCondKernelCDF f Îº Î½) (a : Î±) (q : â„š)
    {s : Set Î²} (hs : MeasurableSet s) :
    âˆ« b in s, stieltjesOfMeasurableRat f hf.measurable (a, b) q âˆ‚(Î½ a)
      = (Îº a (s Ã—Ë¢ Iic (q : â„))).toReal := by
  rw [set_integral_congr_ae hs (g := fun b â†¦ f (a, b) q) ?_, hf.set_integral a hs]
  filter_upwards [stieltjesOfMeasurableRat_ae_eq hf a q] with b hb using fun _ â†¦ hb

lemma set_lintegral_stieltjesOfMeasurableRat_rat [IsFiniteKernel Îº] (hf : IsRatCondKernelCDF f Îº Î½)
    (a : Î±) (q : â„š) {s : Set Î²} (hs : MeasurableSet s) :
    âˆ«â» b in s, ENNReal.ofReal (stieltjesOfMeasurableRat f hf.measurable (a, b) q) âˆ‚(Î½ a)
      = Îº a (s Ã—Ë¢ Iic (q : â„)) := by
  rw [â† ofReal_integral_eq_lintegral_ofReal]
  Â· rw [set_integral_stieltjesOfMeasurableRat_rat hf a q hs, ENNReal.ofReal_toReal]
    exact measure_ne_top _ _
  Â· refine Integrable.restrict ?_
    rw [integrable_congr (stieltjesOfMeasurableRat_ae_eq hf a q)]
    exact hf.integrable a q
  Â· exact ae_of_all _ (fun x â†¦ stieltjesOfMeasurableRat_nonneg _ _ _)

lemma set_lintegral_stieltjesOfMeasurableRat [IsFiniteKernel Îº] (hf : IsRatCondKernelCDF f Îº Î½)
    (a : Î±) (x : â„) {s : Set Î²} (hs : MeasurableSet s) :
    âˆ«â» b in s, ENNReal.ofReal (stieltjesOfMeasurableRat f hf.measurable (a, b) x) âˆ‚(Î½ a)
      = Îº a (s Ã—Ë¢ Iic x) := by
  -- We have the result for `x : â„š` thanks to `set_lintegral_stieltjesOfMeasurableRat_rat`.
  -- We use a monotone convergence argument to extend it to the reals.
  by_cases hÏ_zero : (Î½ a).restrict s = 0
  Â· rw [hÏ_zero, lintegral_zero_measure]
    have âŸ¨q, hqâŸ© := exists_rat_gt x
    suffices Îº a (s Ã—Ë¢ Iic (q : â„)) = 0 by
      symm
      refine measure_mono_null (fun p â†¦ ?_) this
      simp only [mem_prod, mem_Iic, and_imp]
      exact fun h1 h2 â†¦ âŸ¨h1, h2.trans hq.leâŸ©
    suffices (Îº a (s Ã—Ë¢ Iic (q : â„))).toReal = 0 by
      rw [ENNReal.toReal_eq_zero_iff] at this
      simpa [measure_ne_top] using this
    rw [â† hf.set_integral a hs q]
    simp [hÏ_zero]
  have h : âˆ«â» b in s, ENNReal.ofReal (stieltjesOfMeasurableRat f hf.measurable (a, b) x) âˆ‚(Î½ a)
      = âˆ«â» b in s, â¨… r : { r' : â„š // x < r' },
        ENNReal.ofReal (stieltjesOfMeasurableRat f hf.measurable (a, b) r) âˆ‚(Î½ a) := by
    congr with b : 1
    simp_rw [â† measure_stieltjesOfMeasurableRat_Iic]
    rw [â† measure_iInter_eq_iInf]
    Â· congr with y : 1
      simp only [mem_Iic, mem_iInter, Subtype.forall]
      refine âŸ¨fun h a ha â†¦ h.trans ?_, fun h â†¦ ?_âŸ©
      Â· exact mod_cast ha.le
      Â· refine le_of_forall_lt_rat_imp_le fun q hq â†¦ h q ?_
        exact mod_cast hq
    Â· exact fun _ â†¦ measurableSet_Iic
    Â· refine Monotone.directed_ge fun r r' hrr' â†¦ Iic_subset_Iic.mpr ?_
      exact mod_cast hrr'
    Â· obtain âŸ¨q, hqâŸ© := exists_rat_gt x
      exact âŸ¨âŸ¨q, hqâŸ©, measure_ne_top _ _âŸ©
  have h_nonempty : Nonempty { r' : â„š // x < â†‘r' } := by
    obtain âŸ¨r, hrxâŸ© := exists_rat_gt x
    exact âŸ¨âŸ¨r, hrxâŸ©âŸ©
  rw [h, lintegral_iInf_directed_of_measurable hÏ_zero fun q : { r' : â„š // x < â†‘r' } â†¦ ?_]
  rotate_left
  Â· intro b
    rw [set_lintegral_stieltjesOfMeasurableRat_rat hf a _ hs]
    exact measure_ne_top _ _
  Â· refine Monotone.directed_ge fun i j hij b â†¦ ?_
    simp_rw [â† measure_stieltjesOfMeasurableRat_Iic]
    refine measure_mono (Iic_subset_Iic.mpr ?_)
    exact mod_cast hij
  Â· refine Measurable.ennreal_ofReal ?_
    exact (measurable_stieltjesOfMeasurableRat hf.measurable _).comp measurable_prod_mk_left
  simp_rw [set_lintegral_stieltjesOfMeasurableRat_rat hf _ _ hs]
  rw [â† measure_iInter_eq_iInf]
  Â· rw [â† prod_iInter]
    congr with y
    simp only [mem_iInter, mem_Iic, Subtype.forall, Subtype.coe_mk]
    exact âŸ¨le_of_forall_lt_rat_imp_le, fun hyx q hq â†¦ hyx.trans hq.leâŸ©
  Â· exact fun i â†¦ hs.prod measurableSet_Iic
  Â· refine Monotone.directed_ge fun i j hij â†¦ ?_
    refine prod_subset_prod_iff.mpr (Or.inl âŸ¨subset_rfl, Iic_subset_Iic.mpr ?_âŸ©)
    exact mod_cast hij
  Â· exact âŸ¨h_nonempty.some, measure_ne_top _ _âŸ©

lemma lintegral_stieltjesOfMeasurableRat [IsFiniteKernel Îº] (hf : IsRatCondKernelCDF f Îº Î½)
    (a : Î±) (x : â„) :
    âˆ«â» b, ENNReal.ofReal (stieltjesOfMeasurableRat f hf.measurable (a, b) x) âˆ‚(Î½ a)
      = Îº a (univ Ã—Ë¢ Iic x) := by
  rw [â† set_lintegral_univ, set_lintegral_stieltjesOfMeasurableRat hf _ _ MeasurableSet.univ]

lemma integrable_stieltjesOfMeasurableRat [IsFiniteKernel Îº] (hf : IsRatCondKernelCDF f Îº Î½)
    (a : Î±) (x : â„) :
    Integrable (fun b â†¦ stieltjesOfMeasurableRat f hf.measurable (a, b) x) (Î½ a) := by
  have : (fun b â†¦ stieltjesOfMeasurableRat f hf.measurable (a, b) x)
      = fun b â†¦ (ENNReal.ofReal (stieltjesOfMeasurableRat f hf.measurable (a, b) x)).toReal := by
    ext t
    rw [ENNReal.toReal_ofReal]
    exact stieltjesOfMeasurableRat_nonneg _ _ _
  rw [this]
  refine integrable_toReal_of_lintegral_ne_top ?_ ?_
  Â· refine (Measurable.ennreal_ofReal ?_).aemeasurable
    exact (measurable_stieltjesOfMeasurableRat hf.measurable x).comp measurable_prod_mk_left
  Â· rw [lintegral_stieltjesOfMeasurableRat hf]
    exact measure_ne_top _ _

lemma set_integral_stieltjesOfMeasurableRat [IsFiniteKernel Îº] (hf : IsRatCondKernelCDF f Îº Î½)
    (a : Î±) (x : â„) {s : Set Î²} (hs : MeasurableSet s) :
    âˆ« b in s, stieltjesOfMeasurableRat f hf.measurable (a, b) x âˆ‚(Î½ a)
      = (Îº a (s Ã—Ë¢ Iic x)).toReal := by
  rw [â† ENNReal.ofReal_eq_ofReal_iff, ENNReal.ofReal_toReal]
  rotate_left
  Â· exact measure_ne_top _ _
  Â· exact set_integral_nonneg hs (fun _ _ â†¦ stieltjesOfMeasurableRat_nonneg _ _ _)
  Â· exact ENNReal.toReal_nonneg
  rw [ofReal_integral_eq_lintegral_ofReal, set_lintegral_stieltjesOfMeasurableRat hf _ _ hs]
  Â· exact (integrable_stieltjesOfMeasurableRat hf _ _).restrict
  Â· exact ae_of_all _ (fun _ â†¦ stieltjesOfMeasurableRat_nonneg _ _ _)

lemma integral_stieltjesOfMeasurableRat [IsFiniteKernel Îº] (hf : IsRatCondKernelCDF f Îº Î½)
    (a : Î±) (x : â„) :
    âˆ« b, stieltjesOfMeasurableRat f hf.measurable (a, b) x âˆ‚(Î½ a)
      = (Îº a (univ Ã—Ë¢ Iic x)).toReal := by
  rw [â† integral_univ, set_integral_stieltjesOfMeasurableRat hf _ _ MeasurableSet.univ]

end stieltjesOfMeasurableRat

section isRatCondKernelCDFAux

variable {f : Î± Ã— Î² â†’ â„š â†’ â„}

/-- This property implies `IsRatCondKernelCDF`. The measurability, integrability and integral
conditions are the same, but the limit properties of `IsRatCondKernelCDF` are replaced by
limits of integrals. -/
structure IsRatCondKernelCDFAux (f : Î± Ã— Î² â†’ â„š â†’ â„) (Îº : kernel Î± (Î² Ã— â„)) (Î½ : kernel Î± Î²) :
    Prop :=
  (measurable : Measurable f)
  (mono' (a : Î±) {q r : â„š} (_hqr : q â‰¤ r) : âˆ€áµ c âˆ‚(Î½ a), f (a, c) q â‰¤ f (a, c) r)
  (nonneg' (a : Î±) (q : â„š) : âˆ€áµ c âˆ‚(Î½ a), 0 â‰¤ f (a, c) q)
  (le_one' (a : Î±) (q : â„š) : âˆ€áµ c âˆ‚(Î½ a), f (a, c) q â‰¤ 1)
  /- Same as `Tendsto (fun q : â„š â†¦ âˆ« c, f (a, c) q âˆ‚(Î½ a)) atBot (ğ“ 0)` but slightly easier
  to prove in the current applications of this definition (some integral convergence lemmas
  currently apply only to `â„•`, not `â„š`) -/
  (tendsto_integral_of_antitone (a : Î±) (seq : â„• â†’ â„š) (_hs : Antitone seq)
    (_hs_tendsto : Tendsto seq atTop atBot) :
    Tendsto (fun m â†¦ âˆ« c, f (a, c) (seq m) âˆ‚(Î½ a)) atTop (ğ“ 0))
  /- Same as `Tendsto (fun q : â„š â†¦ âˆ« c, f (a, c) q âˆ‚(Î½ a)) atTop (ğ“ (Î½ a univ).toReal)` but
  slightly easier to prove in the current applications of this definition (some integral convergence
  lemmas currently apply only to `â„•`, not `â„š`) -/
  (tendsto_integral_of_monotone (a : Î±) (seq : â„• â†’ â„š) (_hs : Monotone seq)
    (_hs_tendsto : Tendsto seq atTop atTop) :
    Tendsto (fun m â†¦ âˆ« c, f (a, c) (seq m) âˆ‚(Î½ a)) atTop (ğ“ (Î½ a univ).toReal))
  (integrable (a : Î±) (q : â„š) : Integrable (fun c â†¦ f (a, c) q) (Î½ a))
  (set_integral (a : Î±) {A : Set Î²} (_hA : MeasurableSet A) (q : â„š) :
    âˆ« c in A, f (a, c) q âˆ‚(Î½ a) = (Îº a (A Ã—Ë¢ Iic â†‘q)).toReal)

lemma IsRatCondKernelCDFAux.measurable_right (hf : IsRatCondKernelCDFAux f Îº Î½) (a : Î±) (q : â„š) :
    Measurable (fun t â†¦ f (a, t) q) := by
  let h := hf.measurable
  rw [measurable_pi_iff] at h
  exact (h q).comp measurable_prod_mk_left

lemma IsRatCondKernelCDFAux.mono (hf : IsRatCondKernelCDFAux f Îº Î½) (a : Î±) :
    âˆ€áµ c âˆ‚(Î½ a), Monotone (f (a, c)) := by
  unfold Monotone
  simp_rw [ae_all_iff]
  exact fun _ _ hqr â†¦ hf.mono' a hqr

lemma IsRatCondKernelCDFAux.nonneg (hf : IsRatCondKernelCDFAux f Îº Î½) (a : Î±) :
    âˆ€áµ c âˆ‚(Î½ a), âˆ€ q, 0 â‰¤ f (a, c) q := ae_all_iff.mpr <| hf.nonneg' a

lemma IsRatCondKernelCDFAux.le_one (hf : IsRatCondKernelCDFAux f Îº Î½) (a : Î±) :
    âˆ€áµ c âˆ‚(Î½ a), âˆ€ q, f (a, c) q â‰¤ 1 := ae_all_iff.mpr <| hf.le_one' a

lemma IsRatCondKernelCDFAux.tendsto_zero_of_antitone (hf : IsRatCondKernelCDFAux f Îº Î½)
    [IsFiniteKernel Î½] (a : Î±) (seq : â„• â†’ â„š) (hseq : Antitone seq)
    (hseq_tendsto : Tendsto seq atTop atBot) :
    âˆ€áµ c âˆ‚(Î½ a), Tendsto (fun m â†¦ f (a, c) (seq m)) atTop (ğ“ 0) := by
  refine tendsto_of_integral_tendsto_of_antitone ?_ (integrable_const _) ?_ ?_ ?_
  Â· exact fun n â†¦ hf.integrable a (seq n)
  Â· rw [integral_zero]
    exact hf.tendsto_integral_of_antitone a seq hseq hseq_tendsto
  Â· filter_upwards [hf.mono a] with t ht using fun n m hnm â†¦ ht (hseq hnm)
  Â· filter_upwards [hf.nonneg a] with c hc using fun i â†¦ hc (seq i)

lemma IsRatCondKernelCDFAux.tendsto_one_of_monotone (hf : IsRatCondKernelCDFAux f Îº Î½)
    [IsFiniteKernel Î½] (a : Î±) (seq : â„• â†’ â„š) (hseq : Monotone seq)
    (hseq_tendsto : Tendsto seq atTop atTop) :
    âˆ€áµ c âˆ‚(Î½ a), Tendsto (fun m â†¦ f (a, c) (seq m)) atTop (ğ“ 1) := by
  refine tendsto_of_integral_tendsto_of_monotone ?_ (integrable_const _) ?_ ?_ ?_
  Â· exact fun n â†¦ hf.integrable a (seq n)
  Â· rw [MeasureTheory.integral_const, smul_eq_mul, mul_one]
    exact hf.tendsto_integral_of_monotone a seq hseq hseq_tendsto
  Â· filter_upwards [hf.mono a] with t ht using fun n m hnm â†¦ ht (hseq hnm)
  Â· filter_upwards [hf.le_one a] with c hc using fun i â†¦ hc (seq i)

lemma IsRatCondKernelCDFAux.tendsto_atTop_one (hf : IsRatCondKernelCDFAux f Îº Î½) [IsFiniteKernel Î½]
    (a : Î±) :
    âˆ€áµ t âˆ‚(Î½ a), Tendsto (f (a, t)) atTop (ğ“ 1) := by
  suffices âˆ€áµ t âˆ‚(Î½ a), Tendsto (fun (n : â„•) â†¦ f (a, t) n) atTop (ğ“ 1) by
    filter_upwards [this, hf.mono a] with t ht h_mono
    rw [tendsto_iff_tendsto_subseq_of_monotone h_mono tendsto_nat_cast_atTop_atTop]
    exact ht
  let seq : â„• â†’ â„š := fun n â†¦ n
  have hseq : Monotone seq := fun i j hij â†¦ by simp [seq, hij]
  have hseq_tendsto : Tendsto seq atTop atTop := tendsto_nat_cast_atTop_atTop
  filter_upwards [hf.tendsto_one_of_monotone a seq hseq hseq_tendsto] with x hx using hx

lemma IsRatCondKernelCDFAux.tendsto_atBot_zero (hf : IsRatCondKernelCDFAux f Îº Î½) [IsFiniteKernel Î½]
    (a : Î±) :
    âˆ€áµ t âˆ‚(Î½ a), Tendsto (f (a, t)) atBot (ğ“ 0) := by
  suffices âˆ€áµ t âˆ‚(Î½ a), Tendsto (fun q : â„š â†¦ f (a, t) (-q)) atTop (ğ“ 0) by
    filter_upwards [this] with t ht
    have h_eq_neg : f (a, t) = fun q : â„š â†¦ f (a, t) (- -q) := by
      simp_rw [neg_neg]
    rw [h_eq_neg]
    convert ht.comp tendsto_neg_atBot_atTop
    simp
  suffices âˆ€áµ t âˆ‚(Î½ a), Tendsto (fun (n : â„•) â†¦ f (a, t) (-n)) atTop (ğ“ 0) by
    filter_upwards [this, hf.mono a] with t ht h_mono
    have h_anti : Antitone (fun q â†¦ f (a, t) (-q)) := h_mono.comp_antitone monotone_id.neg
    exact (tendsto_iff_tendsto_subseq_of_antitone h_anti tendsto_nat_cast_atTop_atTop).mpr ht
  let seq : â„• â†’ â„š := fun n â†¦ -n
  have hseq : Antitone seq := fun i j hij â†¦ neg_le_neg (by exact mod_cast hij)
  have hseq_tendsto : Tendsto seq atTop atBot := by
    simp only [seq, tendsto_neg_atBot_iff]
    exact tendsto_nat_cast_atTop_atTop
  convert hf.tendsto_zero_of_antitone a seq hseq hseq_tendsto with x n

lemma IsRatCondKernelCDFAux.bddBelow_range (hf : IsRatCondKernelCDFAux f Îº Î½) (a : Î±) :
    âˆ€áµ t âˆ‚(Î½ a), âˆ€ q : â„š, BddBelow (range fun (r : Ioi q) â†¦ f (a, t) r) := by
  filter_upwards [hf.nonneg a] with c hc
  refine fun q â†¦ âŸ¨0, ?_âŸ©
  rw [mem_lowerBounds]
  rintro x âŸ¨y, rflâŸ©
  exact hc y

lemma IsRatCondKernelCDFAux.integrable_iInf_rat_gt (hf : IsRatCondKernelCDFAux f Îº Î½)
    [IsFiniteKernel Î½] (a : Î±) (q : â„š) :
    Integrable (fun t â†¦ â¨… r : Ioi q, f (a, t) r) (Î½ a) := by
  rw [â† memâ„’p_one_iff_integrable]
  refine âŸ¨(measurable_iInf fun i â†¦ hf.measurable_right a _).aestronglyMeasurable, ?_âŸ©
  refine (?_ : _ â‰¤ (Î½ a univ : â„â‰¥0âˆ)).trans_lt (measure_lt_top _ _)
  refine (snorm_le_of_ae_bound (C := 1) ?_).trans (by simp)
  Â· filter_upwards [hf.bddBelow_range a, hf.nonneg a, hf.le_one a]
      with t hbdd_below h_nonneg h_le_one
    rw [Real.norm_eq_abs, abs_of_nonneg]
    Â· refine ciInf_le_of_le ?_ ?_ ?_
      Â· exact hbdd_below _
      Â· exact âŸ¨q + 1, by simpâŸ©
      Â· exact h_le_one _
    Â· exact le_ciInf fun r â†¦ h_nonneg _

lemma _root_.MeasureTheory.Measure.iInf_rat_gt_prod_Iic {Ï : Measure (Î± Ã— â„)} [IsFiniteMeasure Ï]
    {s : Set Î±} (hs : MeasurableSet s) (t : â„š) :
    â¨… r : { r' : â„š // t < r' }, Ï (s Ã—Ë¢ Iic (r : â„)) = Ï (s Ã—Ë¢ Iic (t : â„)) := by
  rw [â† measure_iInter_eq_iInf]
  Â· rw [â† prod_iInter]
    congr with x : 1
    simp only [mem_iInter, mem_Iic, Subtype.forall, Subtype.coe_mk]
    refine âŸ¨fun h â†¦ ?_, fun h a hta â†¦ h.trans ?_âŸ©
    Â· refine le_of_forall_lt_rat_imp_le fun q htq â†¦ h q ?_
      exact mod_cast htq
    Â· exact mod_cast hta.le
  Â· exact fun _ => hs.prod measurableSet_Iic
  Â· refine Monotone.directed_ge fun r r' hrr' â†¦ prod_subset_prod_iff.mpr (Or.inl âŸ¨subset_rfl, ?_âŸ©)
    refine Iic_subset_Iic.mpr ?_
    exact mod_cast hrr'
  Â· exact âŸ¨âŸ¨t + 1, lt_add_one _âŸ©, measure_ne_top Ï _âŸ©

lemma IsRatCondKernelCDFAux.set_integral_iInf_rat_gt (hf : IsRatCondKernelCDFAux f Îº Î½)
    [IsFiniteKernel Îº] [IsFiniteKernel Î½] (a : Î±) (q : â„š) {A : Set Î²} (hA : MeasurableSet A) :
    âˆ« t in A, â¨… r : Ioi q, f (a, t) r âˆ‚(Î½ a) = (Îº a (A Ã—Ë¢ Iic (q : â„))).toReal := by
  refine le_antisymm ?_ ?_
  Â· have h : âˆ€ r : Ioi q, âˆ« t in A, â¨… r' : Ioi q, f (a, t) r' âˆ‚(Î½ a)
        â‰¤ (Îº a (A Ã—Ë¢ Iic (r : â„))).toReal := by
      intro r
      rw [â† hf.set_integral a hA]
      refine set_integral_mono_ae ?_ ?_ ?_
      Â· exact (hf.integrable_iInf_rat_gt _ _).integrableOn
      Â· exact (hf.integrable _ _).integrableOn
      Â· filter_upwards [hf.bddBelow_range a] with t ht using ciInf_le (ht _) r
    calc âˆ« t in A, â¨… r : Ioi q, f (a, t) r âˆ‚(Î½ a)
      â‰¤ â¨… r : Ioi q, (Îº a (A Ã—Ë¢ Iic (r : â„))).toReal := le_ciInf h
    _ = (Îº a (A Ã—Ë¢ Iic (q : â„))).toReal := by
        rw [â† Measure.iInf_rat_gt_prod_Iic hA q]
        exact (ENNReal.toReal_iInf (fun r â†¦ measure_ne_top _ _)).symm
  Â· rw [â† hf.set_integral a hA]
    refine set_integral_mono_ae ?_ ?_ ?_
    Â· exact (hf.integrable _ _).integrableOn
    Â· exact (hf.integrable_iInf_rat_gt _ _).integrableOn
    Â· filter_upwards [hf.mono a] with c h_mono using le_ciInf (fun r â†¦ h_mono (le_of_lt r.prop))

lemma IsRatCondKernelCDFAux.iInf_rat_gt_eq (hf : IsRatCondKernelCDFAux f Îº Î½) [IsFiniteKernel Îº]
    [IsFiniteKernel Î½] (a : Î±) :
    âˆ€áµ t âˆ‚(Î½ a), âˆ€ q : â„š, â¨… r : Ioi q, f (a, t) r = f (a, t) q := by
  rw [ae_all_iff]
  refine fun q â†¦ ae_eq_of_forall_set_integral_eq_of_sigmaFinite (Î¼ := Î½ a) ?_ ?_ ?_
  Â· exact fun _ _ _ â†¦ (hf.integrable_iInf_rat_gt _ _).integrableOn
  Â· exact fun _ _ _ â†¦ (hf.integrable a _).integrableOn
  Â· intro s hs _
    rw [hf.set_integral _ hs, hf.set_integral_iInf_rat_gt _ _ hs]

lemma IsRatCondKernelCDFAux.isRatStieltjesPoint_ae (hf : IsRatCondKernelCDFAux f Îº Î½)
    [IsFiniteKernel Îº] [IsFiniteKernel Î½] (a : Î±) :
    âˆ€áµ t âˆ‚(Î½ a), IsRatStieltjesPoint f (a, t) := by
  filter_upwards [hf.tendsto_atTop_one a, hf.tendsto_atBot_zero a,
    hf.iInf_rat_gt_eq a, hf.mono a] with t ht_top ht_bot ht_iInf h_mono
  exact âŸ¨h_mono, ht_top, ht_bot, ht_iInfâŸ©

lemma IsRatCondKernelCDFAux.isRatCondKernelCDF (hf : IsRatCondKernelCDFAux f Îº Î½) [IsFiniteKernel Îº]
    [IsFiniteKernel Î½] :
    IsRatCondKernelCDF f Îº Î½ where
  measurable := hf.measurable
  isRatStieltjesPoint_ae := hf.isRatStieltjesPoint_ae
  integrable := hf.integrable
  set_integral := hf.set_integral

end isRatCondKernelCDFAux

section IsCondKernelCDF

variable {f : Î± Ã— Î² â†’ StieltjesFunction}

/-- A function `f : Î± Ã— Î² â†’ StieltjesFunction` is called a conditional kernel CDF of `Îº` with
respect to `Î½` if it is measurable, tends to to 0 at -âˆ and to 1 at +âˆ for all `p : Î± Ã— Î²`,
`fun b â†¦ f (a, b) x` is `(Î½ a)`-integrable for all `a : Î±` and `x : â„` and for all
measurable sets `s : Set Î²`, `âˆ« b in s, f (a, b) x âˆ‚(Î½ a) = (Îº a (s Ã—Ë¢ Iic x)).toReal`. -/
structure IsCondKernelCDF (f : Î± Ã— Î² â†’ StieltjesFunction) (Îº : kernel Î± (Î² Ã— â„)) (Î½ : kernel Î± Î²) :
    Prop :=
  (measurable (x : â„) : Measurable fun p â†¦ f p x)
  (integrable (a : Î±) (x : â„) : Integrable (fun b â†¦ f (a, b) x) (Î½ a))
  (tendsto_atTop_one (p : Î± Ã— Î²) : Tendsto (f p) atTop (ğ“ 1))
  (tendsto_atBot_zero (p : Î± Ã— Î²) : Tendsto (f p) atBot (ğ“ 0))
  (set_integral (a : Î±) {s : Set Î²} (_hs : MeasurableSet s) (x : â„) :
    âˆ« b in s, f (a, b) x âˆ‚(Î½ a) = (Îº a (s Ã—Ë¢ Iic x)).toReal)

lemma IsCondKernelCDF.nonneg (hf : IsCondKernelCDF f Îº Î½) (p : Î± Ã— Î²) (x : â„) : 0 â‰¤ f p x :=
  Monotone.le_of_tendsto (f p).mono (hf.tendsto_atBot_zero p) x

lemma IsCondKernelCDF.le_one (hf : IsCondKernelCDF f Îº Î½) (p : Î± Ã— Î²) (x : â„) : f p x â‰¤ 1 :=
  Monotone.ge_of_tendsto (f p).mono (hf.tendsto_atTop_one p) x

lemma IsCondKernelCDF.integral
    {f : Î± Ã— Î² â†’ StieltjesFunction} (hf : IsCondKernelCDF f Îº Î½) (a : Î±) (x : â„) :
    âˆ« b, f (a, b) x âˆ‚(Î½ a) = (Îº a (univ Ã—Ë¢ Iic x)).toReal := by
  rw [â† hf.set_integral _ MeasurableSet.univ, Measure.restrict_univ]

lemma IsCondKernelCDF.set_lintegral [IsFiniteKernel Îº]
    {f : Î± Ã— Î² â†’ StieltjesFunction} (hf : IsCondKernelCDF f Îº Î½)
    (a : Î±) {s : Set Î²} (hs : MeasurableSet s) (x : â„) :
    âˆ«â» b in s, ENNReal.ofReal (f (a, b) x) âˆ‚(Î½ a) = Îº a (s Ã—Ë¢ Iic x) := by
  rw [â† ofReal_integral_eq_lintegral_ofReal (hf.integrable a x).restrict
    (ae_of_all _ (fun _ â†¦ hf.nonneg _ _)), hf.set_integral a hs x, ENNReal.ofReal_toReal]
  exact measure_ne_top _ _

lemma IsCondKernelCDF.lintegral [IsFiniteKernel Îº]
    {f : Î± Ã— Î² â†’ StieltjesFunction} (hf : IsCondKernelCDF f Îº Î½) (a : Î±) (x : â„) :
    âˆ«â» b, ENNReal.ofReal (f (a, b) x) âˆ‚(Î½ a) = Îº a (univ Ã—Ë¢ Iic x) := by
  rw [â† hf.set_lintegral _ MeasurableSet.univ, Measure.restrict_univ]

lemma isCondKernelCDF_stieltjesOfMeasurableRat {f : Î± Ã— Î² â†’ â„š â†’ â„} (hf : IsRatCondKernelCDF f Îº Î½)
    [IsFiniteKernel Îº] :
    IsCondKernelCDF (stieltjesOfMeasurableRat f hf.measurable) Îº Î½ where
  measurable := measurable_stieltjesOfMeasurableRat hf.measurable
  integrable := integrable_stieltjesOfMeasurableRat hf
  tendsto_atTop_one := tendsto_stieltjesOfMeasurableRat_atTop hf.measurable
  tendsto_atBot_zero := tendsto_stieltjesOfMeasurableRat_atBot hf.measurable
  set_integral a _ hs x := set_integral_stieltjesOfMeasurableRat hf a x hs

end IsCondKernelCDF

section ToKernel

variable {_ : MeasurableSpace Î²} {f : Î± Ã— Î² â†’ StieltjesFunction}
  {Îº : kernel Î± (Î² Ã— â„)} {Î½ : kernel Î± Î²}

/-- A measurable function `Î± â†’ StieltjesFunction` with limits 0 at -âˆ and 1 at +âˆ gives a measurable
function `Î± â†’ Measure â„` by taking `StieltjesFunction.measure` at each point. -/
lemma StieltjesFunction.measurable_measure {f : Î± â†’ StieltjesFunction}
    (hf : âˆ€ q, Measurable fun a â†¦ f a q)
    (hf_bot : âˆ€ a, Tendsto (f a) atBot (ğ“ 0))
    (hf_top : âˆ€ a, Tendsto (f a) atTop (ğ“ 1)) :
    Measurable fun a â†¦ (f a).measure := by
  refine Measure.measurable_measure.mpr fun s hs â†¦ ?_
  have : âˆ€ a, IsProbabilityMeasure (f a).measure :=
    fun a â†¦ (f a).isProbabilityMeasure (hf_bot a) (hf_top a)
  refine MeasurableSpace.induction_on_inter
    (C := fun s â†¦ Measurable fun b â†¦ StieltjesFunction.measure (f b) s)
    (borel_eq_generateFrom_Iic â„) isPiSystem_Iic ?_ ?_ ?_ ?_ hs
  Â· simp only [measure_empty, measurable_const]
  Â· rintro S âŸ¨u, rflâŸ©
    simp_rw [StieltjesFunction.measure_Iic (f _) (hf_bot _)]
    simp only [sub_zero]
    exact (hf _).ennreal_ofReal
  Â· intro t ht ht_cd_meas
    have : (fun a â†¦ (f a).measure tá¶œ) =
        (fun a â†¦ (f a).measure univ)
          - fun a â†¦ (f a).measure t := by
      ext1 a
      rw [measure_compl ht, Pi.sub_apply]
      exact measure_ne_top _ _
    simp_rw [this, measure_univ]
    exact Measurable.sub measurable_const ht_cd_meas
  Â· intro f hf_disj hf_meas hf_cd_meas
    simp_rw [measure_iUnion hf_disj hf_meas]
    exact Measurable.ennreal_tsum hf_cd_meas

/-- A function `f : Î± Ã— Î² â†’ StieltjesFunction` with the property `IsCondKernelCDF f Îº Î½` gives a
Markov kernel from `Î± Ã— Î²` to `â„`, by taking for each `p : Î± Ã— Î²` the measure defined by `f p`. -/
noncomputable
def IsCondKernelCDF.toKernel (f : Î± Ã— Î² â†’ StieltjesFunction) (hf : IsCondKernelCDF f Îº Î½) :
    kernel (Î± Ã— Î²) â„ where
  val p := (f p).measure
  property := StieltjesFunction.measurable_measure hf.measurable
    hf.tendsto_atBot_zero hf.tendsto_atTop_one

lemma IsCondKernelCDF.toKernel_apply {hf : IsCondKernelCDF f Îº Î½} (p : Î± Ã— Î²) :
    hf.toKernel f p = (f p).measure := rfl

instance instIsMarkovKernel_toKernel {hf : IsCondKernelCDF f Îº Î½} :
    IsMarkovKernel (hf.toKernel f) :=
  âŸ¨fun _ â†¦ (f _).isProbabilityMeasure (hf.tendsto_atBot_zero _) (hf.tendsto_atTop_one _)âŸ©

lemma IsCondKernelCDF.toKernel_Iic {hf : IsCondKernelCDF f Îº Î½} (p : Î± Ã— Î²) (x : â„) :
    hf.toKernel f p (Iic x) = ENNReal.ofReal (f p x) := by
  rw [IsCondKernelCDF.toKernel_apply p, (f p).measure_Iic (hf.tendsto_atBot_zero p)]
  simp

end ToKernel

section

variable {f : Î± Ã— Î² â†’ StieltjesFunction}

lemma set_lintegral_toKernel_Iic [IsFiniteKernel Îº] (hf : IsCondKernelCDF f Îº Î½)
    (a : Î±) (x : â„) {s : Set Î²} (hs : MeasurableSet s) :
    âˆ«â» b in s, hf.toKernel f (a, b) (Iic x) âˆ‚(Î½ a) = Îº a (s Ã—Ë¢ Iic x) := by
  simp_rw [IsCondKernelCDF.toKernel_Iic]
  exact hf.set_lintegral _ hs _

lemma set_lintegral_toKernel_univ [IsFiniteKernel Îº] (hf : IsCondKernelCDF f Îº Î½)
    (a : Î±) {s : Set Î²} (hs : MeasurableSet s) :
    âˆ«â» b in s, hf.toKernel f (a, b) univ âˆ‚(Î½ a) = Îº a (s Ã—Ë¢ univ) := by
  rw [â† Real.iUnion_Iic_rat, prod_iUnion]
  have h_dir : Directed (fun x y â†¦ x âŠ† y) fun q : â„š â†¦ Iic (q : â„) := by
    refine Monotone.directed_le fun r r' hrr' â†¦ Iic_subset_Iic.mpr ?_
    exact mod_cast hrr'
  have h_dir_prod : Directed (fun x y â†¦ x âŠ† y) fun q : â„š â†¦ s Ã—Ë¢ Iic (q : â„) := by
    refine Monotone.directed_le fun i j hij â†¦ ?_
    refine prod_subset_prod_iff.mpr (Or.inl âŸ¨subset_rfl, Iic_subset_Iic.mpr ?_âŸ©)
    exact mod_cast hij
  simp_rw [measure_iUnion_eq_iSup h_dir, measure_iUnion_eq_iSup h_dir_prod]
  rw [lintegral_iSup_directed]
  Â· simp_rw [set_lintegral_toKernel_Iic hf _ _ hs]
  Â· refine fun q â†¦ Measurable.aemeasurable ?_
    exact (kernel.measurable_coe _ measurableSet_Iic).comp measurable_prod_mk_left
  Â· refine Monotone.directed_le fun i j hij t â†¦ measure_mono (Iic_subset_Iic.mpr ?_)
    exact mod_cast hij

lemma lintegral_toKernel_univ [IsFiniteKernel Îº] (hf : IsCondKernelCDF f Îº Î½) (a : Î±) :
    âˆ«â» b, hf.toKernel f (a, b) univ âˆ‚(Î½ a) = Îº a univ := by
  rw [â† set_lintegral_univ, set_lintegral_toKernel_univ hf a MeasurableSet.univ, univ_prod_univ]

lemma set_lintegral_toKernel_prod [IsFiniteKernel Îº] (hf : IsCondKernelCDF f Îº Î½)
    (a : Î±) {s : Set Î²} (hs : MeasurableSet s) {t : Set â„} (ht : MeasurableSet t) :
    âˆ«â» b in s, hf.toKernel f (a, b) t âˆ‚(Î½ a) = Îº a (s Ã—Ë¢ t) := by
  -- `set_lintegral_toKernel_Iic` gives the result for `t = Iic x`. These sets form a
  -- Ï€-system that generates the Borel Ïƒ-algebra, hence we can get the same equality for any
  -- measurable set `t`.
  apply MeasurableSpace.induction_on_inter (borel_eq_generateFrom_Iic â„) isPiSystem_Iic _ _ _ _ ht
  Â· simp only [measure_empty, lintegral_const, zero_mul, prod_empty]
  Â· rintro t âŸ¨q, rflâŸ©
    exact set_lintegral_toKernel_Iic hf a _ hs
  Â· intro t ht ht_lintegral
    calc âˆ«â» b in s, hf.toKernel f (a, b) tá¶œ âˆ‚(Î½ a)
      = âˆ«â» b in s, hf.toKernel f (a, b) univ - hf.toKernel f (a, b) t âˆ‚(Î½ a) := by
          congr with x; rw [measure_compl ht (measure_ne_top (hf.toKernel f (a, x)) _)]
    _ = âˆ«â» b in s, hf.toKernel f (a, b) univ âˆ‚(Î½ a)
          - âˆ«â» b in s, hf.toKernel f (a, b) t âˆ‚(Î½ a) := by
        rw [lintegral_sub]
        Â· exact (kernel.measurable_coe (hf.toKernel f) ht).comp measurable_prod_mk_left
        Â· rw [ht_lintegral]
          exact measure_ne_top _ _
        Â· exact eventually_of_forall fun a â†¦ measure_mono (subset_univ _)
    _ = Îº a (s Ã—Ë¢ univ) - Îº a (s Ã—Ë¢ t) := by
        rw [set_lintegral_toKernel_univ hf a hs, ht_lintegral]
    _ = Îº a (s Ã—Ë¢ tá¶œ) := by
        rw [â† measure_diff _ (hs.prod ht) (measure_ne_top _ _)]
        Â· rw [prod_diff_prod, compl_eq_univ_diff]
          simp only [diff_self, empty_prod, union_empty]
        Â· rw [prod_subset_prod_iff]
          exact Or.inl âŸ¨subset_rfl, subset_univ tâŸ©
  Â· intro f hf_disj hf_meas hf_eq
    simp_rw [measure_iUnion hf_disj hf_meas]
    rw [lintegral_tsum, prod_iUnion, measure_iUnion]
    Â· simp_rw [hf_eq]
    Â· intro i j hij
      rw [Function.onFun, Set.disjoint_prod]
      exact Or.inr (hf_disj hij)
    Â· exact fun i â†¦ MeasurableSet.prod hs (hf_meas i)
    Â· exact fun i â†¦
        ((kernel.measurable_coe _ (hf_meas i)).comp measurable_prod_mk_left).aemeasurable.restrict

lemma lintegral_toKernel_mem [IsFiniteKernel Îº] (hf : IsCondKernelCDF f Îº Î½)
    (a : Î±) {s : Set (Î² Ã— â„)} (hs : MeasurableSet s) :
    âˆ«â» b, hf.toKernel f (a, b) {y | (b, y) âˆˆ s} âˆ‚(Î½ a) = Îº a s := by
  -- `set_lintegral_toKernel_prod` gives the result for sets of the form `tâ‚ Ã— tâ‚‚`. These
  -- sets form a Ï€-system that generates the product Ïƒ-algebra, hence we can get the same equality
  -- for any measurable set `s`.
  apply MeasurableSpace.induction_on_inter generateFrom_prod.symm isPiSystem_prod _ _ _ _ hs
  Â· simp only [mem_empty_iff_false, setOf_false, measure_empty, lintegral_const,
      zero_mul]
  Â· rintro _ âŸ¨tâ‚, htâ‚, tâ‚‚, htâ‚‚, rflâŸ©
    simp only [mem_setOf_eq] at htâ‚ htâ‚‚
    have h_prod_eq_snd : âˆ€ a âˆˆ tâ‚, {x : â„ | (a, x) âˆˆ tâ‚ Ã—Ë¢ tâ‚‚} = tâ‚‚ := by
      intro a ha
      simp only [ha, prod_mk_mem_set_prod_eq, true_and_iff, setOf_mem_eq]
    rw [â† lintegral_add_compl _ htâ‚]
    have h_eq1 : âˆ«â» x in tâ‚, hf.toKernel f (a, x) {y : â„ | (x, y) âˆˆ tâ‚ Ã—Ë¢ tâ‚‚} âˆ‚(Î½ a)
        = âˆ«â» x in tâ‚, hf.toKernel f (a, x) tâ‚‚ âˆ‚(Î½ a) := by
      refine set_lintegral_congr_fun htâ‚ (eventually_of_forall fun a ha â†¦ ?_)
      rw [h_prod_eq_snd a ha]
    have h_eq2 :
        âˆ«â» x in tâ‚á¶œ, hf.toKernel f (a, x) {y : â„ | (x, y) âˆˆ tâ‚ Ã—Ë¢ tâ‚‚} âˆ‚(Î½ a) = 0 := by
      suffices h_eq_zero :
          âˆ€ x âˆˆ tâ‚á¶œ, hf.toKernel f (a, x) {y : â„ | (x, y) âˆˆ tâ‚ Ã—Ë¢ tâ‚‚} = 0 by
        rw [set_lintegral_congr_fun htâ‚.compl (eventually_of_forall h_eq_zero)]
        simp only [lintegral_const, zero_mul]
      intro a hatâ‚
      rw [mem_compl_iff] at hatâ‚
      simp only [hatâ‚, prod_mk_mem_set_prod_eq, false_and_iff, setOf_false, measure_empty]
    rw [h_eq1, h_eq2, add_zero]
    exact set_lintegral_toKernel_prod hf a htâ‚ htâ‚‚
  Â· intro t ht ht_eq
    calc âˆ«â» b, hf.toKernel f (a, b) {y : â„ | (b, y) âˆˆ tá¶œ} âˆ‚(Î½ a)
      = âˆ«â» b, hf.toKernel f (a, b) {y : â„ | (b, y) âˆˆ t}á¶œ âˆ‚(Î½ a) := rfl
    _ = âˆ«â» b, hf.toKernel f (a, b) univ
          - hf.toKernel f (a, b) {y : â„ | (b, y) âˆˆ t} âˆ‚(Î½ a) := by
        congr with x : 1
        exact measure_compl (measurable_prod_mk_left ht)
          (measure_ne_top (hf.toKernel f (a, x)) _)
    _ = âˆ«â» x, hf.toKernel f (a, x) univ âˆ‚(Î½ a) -
          âˆ«â» x, hf.toKernel f (a, x) {y : â„ | (x, y) âˆˆ t} âˆ‚(Î½ a) := by
        have h_le : (fun x â†¦ hf.toKernel f (a, x) {y : â„ | (x, y) âˆˆ t})
              â‰¤áµ[Î½ a] fun x â†¦ hf.toKernel f (a, x) univ :=
          eventually_of_forall fun _ â†¦ measure_mono (subset_univ _)
        rw [lintegral_sub _ _ h_le]
        Â· exact kernel.measurable_kernel_prod_mk_left' ht a
        refine ((lintegral_mono_ae h_le).trans_lt ?_).ne
        rw [lintegral_toKernel_univ hf]
        exact measure_lt_top _ univ
    _ = Îº a univ - Îº a t := by rw [ht_eq, lintegral_toKernel_univ hf]
    _ = Îº a tá¶œ := (measure_compl ht (measure_ne_top _ _)).symm
  Â· intro f' hf_disj hf_meas hf_eq
    have h_eq : âˆ€ a, {x | (a, x) âˆˆ â‹ƒ i, f' i} = â‹ƒ i, {x | (a, x) âˆˆ f' i} := by
      intro a; ext x; simp only [mem_iUnion, mem_setOf_eq]
    simp_rw [h_eq]
    have h_disj : âˆ€ a, Pairwise (Disjoint on fun i â†¦ {x | (a, x) âˆˆ f' i}) := by
      intro a i j hij
      have h_disj := hf_disj hij
      rw [Function.onFun, disjoint_iff_inter_eq_empty] at h_disj âŠ¢
      ext1 x
      simp only [mem_inter_iff, mem_setOf_eq, mem_empty_iff_false, iff_false_iff]
      intro h_mem_both
      suffices (a, x) âˆˆ âˆ… by rwa [mem_empty_iff_false] at this
      rwa [â† h_disj, mem_inter_iff]
    calc âˆ«â» b, hf.toKernel f (a, b) (â‹ƒ i, {y | (b, y) âˆˆ f' i}) âˆ‚(Î½ a)
      = âˆ«â» b, âˆ‘' i, hf.toKernel f (a, b) {y | (b, y) âˆˆ f' i} âˆ‚(Î½ a) := by
          congr with x : 1
          rw [measure_iUnion (h_disj x) fun i â†¦ measurable_prod_mk_left (hf_meas i)]
    _ = âˆ‘' i, âˆ«â» b, hf.toKernel f (a, b) {y | (b, y) âˆˆ f' i} âˆ‚(Î½ a) :=
          lintegral_tsum fun i â†¦ (kernel.measurable_kernel_prod_mk_left' (hf_meas i) a).aemeasurable
    _ = âˆ‘' i, Îº a (f' i) := by simp_rw [hf_eq]
    _ = Îº a (iUnion f') := (measure_iUnion hf_disj hf_meas).symm

lemma compProd_toKernel [IsFiniteKernel Îº] [IsSFiniteKernel Î½] (hf : IsCondKernelCDF f Îº Î½) :
    Î½ âŠ—â‚– hf.toKernel f = Îº := by
  ext a s hs
  rw [kernel.compProd_apply _ _ _ hs, lintegral_toKernel_mem hf a hs]

lemma ae_toKernel_eq_one [IsFiniteKernel Îº] [IsSFiniteKernel Î½] (hf : IsCondKernelCDF f Îº Î½) (a : Î±)
    {s : Set â„} (hs : MeasurableSet s) (hÎºs : Îº a {x | x.snd âˆˆ sá¶œ} = 0) :
    âˆ€áµ b âˆ‚(Î½ a), hf.toKernel f (a, b) s = 1 := by
  have h_eq : Î½ âŠ—â‚– hf.toKernel f = Îº := compProd_toKernel hf
  have h : Îº a {x | x.snd âˆˆ sá¶œ} = (Î½ âŠ—â‚– hf.toKernel f) a {x | x.snd âˆˆ sá¶œ} := by rw [h_eq]
  rw [hÎºs, kernel.compProd_apply] at h
  swap; Â· exact measurable_snd hs.compl
  rw [eq_comm, lintegral_eq_zero_iff] at h
  swap
  Â· simp only [mem_compl_iff, mem_setOf_eq]
    change Measurable ((fun p â†¦ hf.toKernel f p {c | c âˆ‰ s}) âˆ˜ (fun b : Î² â†¦ (a, b)))
    exact (kernel.measurable_coe _ hs.compl).comp measurable_prod_mk_left
  simp only [mem_compl_iff, mem_setOf_eq, kernel.prodMkLeft_apply'] at h
  filter_upwards [h] with b hb
  change hf.toKernel f (a, b) sá¶œ = 0 at hb
  rwa [prob_compl_eq_zero_iff hs] at hb

lemma measurableSet_toKernel_eq_one (hf : IsCondKernelCDF f Îº Î½)
    {s : Set â„} (hs : MeasurableSet s) :
    MeasurableSet {p | hf.toKernel f p s = 1} :=
  (kernel.measurable_coe _ hs) (measurableSet_singleton 1)

end

end ProbabilityTheory
