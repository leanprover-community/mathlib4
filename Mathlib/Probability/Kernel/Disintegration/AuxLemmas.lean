import Mathlib.MeasureTheory.Constructions.Polish
import Mathlib.MeasureTheory.Integral.Bochner

open Filter Set MeasureTheory

open scoped Topology ENNReal

variable {Î± Î² : Type*} {mÎ± : MeasurableSpace Î±}

theorem Real.iUnion_Iic_rat : â‹ƒ r : â„š, Iic (r : â„) = univ := by
  ext1 x
  simp only [mem_iUnion, mem_Iic, mem_univ, iff_true_iff]
  obtain âŸ¨r, hrâŸ© := exists_rat_gt x
  exact âŸ¨r, hr.leâŸ©
#align real.Union_Iic_rat Real.iUnion_Iic_rat

theorem Real.iInter_Iic_rat : â‹‚ r : â„š, Iic (r : â„) = âˆ… := by
  ext1 x
  simp only [mem_iInter, mem_Iic, mem_empty_iff_false, iff_false_iff, not_forall, not_le]
  exact exists_rat_lt x
#align real.Inter_Iic_rat Real.iInter_Iic_rat

lemma measurableSet_tendsto_nhds {Î² Î³ Î¹ : Type*} [MeasurableSpace Î²]
    [TopologicalSpace Î³] [PolishSpace Î³] [MeasurableSpace Î³]
    [hÎ³ : OpensMeasurableSpace Î³] [Countable Î¹] {l : Filter Î¹}
    [l.IsCountablyGenerated] {f : Î¹ â†’ Î² â†’ Î³} (hf : âˆ€ i, Measurable (f i)) (c : Î³) :
    MeasurableSet { x | Tendsto (fun n â†¦ f n x) l (ğ“ c) } := sorry

lemma measurableSet_tendsto_fun {Î² Î³ Î¹ : Type*} [MeasurableSpace Î²]
    [TopologicalSpace Î³] [PolishSpace Î³] [MeasurableSpace Î³]
    [hÎ³ : OpensMeasurableSpace Î³] [Countable Î¹] {l : Filter Î¹}
    [l.IsCountablyGenerated] {f : Î¹ â†’ Î² â†’ Î³} (hf : âˆ€ i, Measurable (f i)) {g : Î² â†’ Î³}
    (hg : Measurable g) :
    MeasurableSet { x | Tendsto (fun n â†¦ f n x) l (ğ“ (g x)) } := by
  letI := upgradePolishSpace Î³
  have : { x | Tendsto (fun n â†¦ f n x) l (ğ“ (g x)) }
      = { x | Tendsto (fun n â†¦ dist (f n x) (g x)) l (ğ“ 0) } := by
    ext x
    simp only [Set.mem_setOf_eq]
    rw [tendsto_iff_dist_tendsto_zero]
  rw [this]
  exact measurableSet_tendsto_nhds (fun n â†¦ (hf n).dist hg) 0

lemma integral_tendsto_of_tendsto_of_monotone {Î¼ : Measure Î±} {f : â„• â†’ Î± â†’ â„} {F : Î± â†’ â„}
    (hf : âˆ€ n, Integrable (f n) Î¼) (hF : Integrable F Î¼) (h_mono : âˆ€áµ x âˆ‚Î¼, Monotone fun n â†¦ f n x)
    (h_tendsto : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n â†¦ f n x) atTop (ğ“ (F x))) :
    Tendsto (fun n â†¦ âˆ« x, f n x âˆ‚Î¼) atTop (ğ“ (âˆ« x, F x âˆ‚Î¼)) := by
  let f' := fun n x â†¦ f n x - f 0 x
  have hf'_nonneg : âˆ€áµ x âˆ‚Î¼, âˆ€ n, 0 â‰¤ f' n x := by
    filter_upwards [h_mono] with a ha n
    simp [ha (zero_le n)]
  have hf'_meas : âˆ€ n, Integrable (f' n) Î¼ := fun n â†¦ (hf n).sub (hf 0)
  suffices Tendsto (fun n â†¦ âˆ« x, f' n x âˆ‚Î¼) atTop (ğ“ (âˆ« x, (F - f 0) x âˆ‚Î¼)) by
    rw [integral_sub' hF (hf 0)] at this
    have h_sub : âˆ€ n, âˆ« x, f' n x âˆ‚Î¼ = âˆ« x, f n x âˆ‚Î¼ - âˆ« x, f 0 x âˆ‚Î¼ := by
      intro n
      simp only
      rw [integral_sub (hf n) (hf 0)]
    simp_rw [h_sub] at this
    have h1 : (fun n â†¦ âˆ« x, f n x âˆ‚Î¼)
        = fun n â†¦ (âˆ« x, f n x âˆ‚Î¼ - âˆ« x, f 0 x âˆ‚Î¼) + âˆ« x, f 0 x âˆ‚Î¼ := by ext n; abel
    have h2 : âˆ« x, F x âˆ‚Î¼ = (âˆ« x, F x âˆ‚Î¼ - âˆ« x, f 0 x âˆ‚Î¼) + âˆ« x, f 0 x âˆ‚Î¼ := by abel
    rw [h1, h2]
    exact this.add tendsto_const_nhds
  have hF_ge : 0 â‰¤áµ[Î¼] fun x â†¦ (F - f 0) x := by
    filter_upwards [h_tendsto, h_mono] with x hx_tendsto hx_mono
    simp only [Pi.zero_apply, Pi.sub_apply, sub_nonneg]
    exact ge_of_tendsto' hx_tendsto (fun n â†¦ hx_mono (zero_le _))
  rw [ae_all_iff] at hf'_nonneg
  simp_rw [integral_eq_lintegral_of_nonneg_ae (hf'_nonneg _) (hf'_meas _).1]
  rw [integral_eq_lintegral_of_nonneg_ae hF_ge (hF.1.sub (hf 0).1)]
  have h_cont := ENNReal.continuousOn_toReal.continuousAt
    (x := âˆ«â» a, ENNReal.ofReal ((F - f 0) a) âˆ‚Î¼) ?_
  swap
  Â· rw [mem_nhds_iff]
    refine âŸ¨Iio (âˆ«â» a, ENNReal.ofReal ((F - f 0) a) âˆ‚Î¼ + 1), ?_, isOpen_Iio, ?_âŸ©
    Â· intro x
      simp only [Pi.sub_apply, mem_Iio, ne_eq, mem_setOf_eq]
      exact ne_top_of_lt
    Â· simp only [Pi.sub_apply, mem_Iio]
      refine ENNReal.lt_add_right ?_ one_ne_zero
      rw [â† ofReal_integral_eq_lintegral_ofReal]
      Â· exact ENNReal.ofReal_ne_top
      Â· exact hF.sub (hf 0)
      Â· exact hF_ge
  refine h_cont.tendsto.comp ?_
  refine lintegral_tendsto_of_tendsto_of_monotone ?_ ?_ ?_
  Â· exact fun n â†¦ ((hf n).sub (hf 0)).aemeasurable.ennreal_ofReal
  Â· filter_upwards [h_mono] with x hx
    intro n m hnm
    refine ENNReal.ofReal_le_ofReal ?_
    simp only [tsub_le_iff_right, sub_add_cancel]
    exact hx hnm
  Â· filter_upwards [h_tendsto] with x hx
    refine (ENNReal.continuous_ofReal.tendsto _).comp ?_
    simp only [Pi.sub_apply]
    exact Tendsto.sub hx tendsto_const_nhds

lemma integral_tendsto_of_tendsto_of_antitone {Î¼ : Measure Î±} {f : â„• â†’ Î± â†’ â„} {F : Î± â†’ â„}
    (hf : âˆ€ n, Integrable (f n) Î¼) (hF : Integrable F Î¼) (h_mono : âˆ€áµ x âˆ‚Î¼, Antitone fun n â†¦ f n x)
    (h_tendsto : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n â†¦ f n x) atTop (ğ“ (F x))) :
    Tendsto (fun n â†¦ âˆ« x, f n x âˆ‚Î¼) atTop (ğ“ (âˆ« x, F x âˆ‚Î¼)) := by
  suffices Tendsto (fun n â†¦ âˆ« x, -f n x âˆ‚Î¼) atTop (ğ“ (âˆ« x, -F x âˆ‚Î¼)) by
    suffices Tendsto (fun n â†¦ âˆ« x, - -f n x âˆ‚Î¼) atTop (ğ“ (âˆ« x, - -F x âˆ‚Î¼)) by
      simp_rw [neg_neg] at this
      exact this
    convert this.neg <;> rw [integral_neg]
  refine integral_tendsto_of_tendsto_of_monotone (fun n â†¦ (hf n).neg) hF.neg ?_ ?_
  Â· filter_upwards [h_mono] with x hx
    intro n m hnm
    simp only [neg_le_neg_iff]
    exact hx hnm
  Â· filter_upwards [h_tendsto] with x hx
    exact hx.neg

lemma tendsto_nat_ceil_atTop {Î± : Type*} [LinearOrderedSemiring Î±] [FloorSemiring Î±] :
    Tendsto (fun x : Î± â†¦ âŒˆxâŒ‰â‚Š) atTop atTop := by
  refine Nat.ceil_mono.tendsto_atTop_atTop (fun x â†¦ âŸ¨x, ?_âŸ©)
  simp only [Nat.ceil_natCast, le_refl]

lemma isCoboundedUnder_le_of_eventually_le {Î¹ Î± : Type*} [Preorder Î±] (l : Filter Î¹) [NeBot l]
    {f : Î¹ â†’ Î±} {x : Î±} (hf : âˆ€á¶  i in l, x â‰¤ f i) :
    IsCoboundedUnder (Â· â‰¤ Â·) l f :=
  IsBoundedUnder.isCoboundedUnder_le âŸ¨x, hfâŸ©

lemma isCoboundedUnder_le_of_le {Î¹ Î± : Type*} [Preorder Î±] (l : Filter Î¹) [NeBot l] {f : Î¹ â†’ Î±}
    {x : Î±} (hf : âˆ€ i, x â‰¤ f i) :
    IsCoboundedUnder (Â· â‰¤ Â·) l f :=
  isCoboundedUnder_le_of_eventually_le l (eventually_of_forall hf)
