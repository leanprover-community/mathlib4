/-
Copyright (c) 2023 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Measure.Decomposition.RadonNikodym
import Mathlib.MeasureTheory.Measure.Prod
import Mathlib.Probability.Kernel.Disintegration.CDFToKernel

/-!
# Conditional cumulative distribution function

Given `Ï : Measure (Î± Ã— â„)`, we define the conditional cumulative distribution function
(conditional cdf) of `Ï`. It is a function `condCDF Ï : Î± â†’ â„ â†’ â„` such that if `Ï` is a finite
measure, then for all `a : Î±` `condCDF Ï a` is monotone and right-continuous with limit 0 at -âˆ
and limit 1 at +âˆ, and such that for all `x : â„`, `a â†¦ condCDF Ï a x` is measurable. For all
`x : â„` and measurable set `s`, that function satisfies
`âˆ«â» a in s, ENNReal.ofReal (condCDF Ï a x) âˆ‚Ï.fst = Ï (s Ã—Ë¢ Iic x)`.

`condCDF` is build from the more general tools about kernel CDFs developed in the file
`Probability.Kernel.Disintegration.CDFToKernel`. In that file, we build a function
`Î± Ã— Î² â†’ StieltjesFunction` (which is `Î± Ã— Î² â†’ â„ â†’ â„` with additional properties) from a function
`Î± Ã— Î² â†’ â„š â†’ â„`. The restriction to `â„š` allows to prove some properties like measurability more
easily. Here we apply that construction to the case `Î² = Unit` and then drop `Î²` to build
`condCDF : Î± â†’ StieltjesFunction`.

## Main definitions

* `ProbabilityTheory.condCDF Ï : Î± â†’ StieltjesFunction`: the conditional cdf of
  `Ï : Measure (Î± Ã— â„)`. A `StieltjesFunction` is a function `â„ â†’ â„` which is monotone and
  right-continuous.

## Main statements

* `ProbabilityTheory.setLIntegral_condCDF`: for all `a : Î±` and `x : â„`, all measurable set `s`,
  `âˆ«â» a in s, ENNReal.ofReal (condCDF Ï a x) âˆ‚Ï.fst = Ï (s Ã—Ë¢ Iic x)`.

-/

open MeasureTheory Set Filter TopologicalSpace

open scoped NNReal ENNReal MeasureTheory Topology

namespace MeasureTheory.Measure

variable {Î± : Type*} {mÎ± : MeasurableSpace Î±} (Ï : Measure (Î± Ã— â„))

/-- Measure on `Î±` such that for a measurable set `s`, `Ï.IicSnd r s = Ï (s Ã—Ë¢ Iic r)`. -/
noncomputable def IicSnd (r : â„) : Measure Î± :=
  (Ï.restrict (univ Ã—Ë¢ Iic r)).fst

theorem IicSnd_apply (r : â„) {s : Set Î±} (hs : MeasurableSet s) :
    Ï.IicSnd r s = Ï (s Ã—Ë¢ Iic r) := by
  rw [IicSnd, fst_apply hs, restrict_apply' (MeasurableSet.univ.prod measurableSet_Iic),
    univ_prod, Set.prod_eq]

theorem IicSnd_univ (r : â„) : Ï.IicSnd r univ = Ï (univ Ã—Ë¢ Iic r) :=
  IicSnd_apply Ï r MeasurableSet.univ

@[gcongr]
theorem IicSnd_mono {r r' : â„} (h_le : r â‰¤ r') : Ï.IicSnd r â‰¤ Ï.IicSnd r' := by
  unfold IicSnd; gcongr

theorem IicSnd_le_fst (r : â„) : Ï.IicSnd r â‰¤ Ï.fst :=
  fst_mono restrict_le_self

theorem IicSnd_ac_fst (r : â„) : Ï.IicSnd r â‰ª Ï.fst :=
  Measure.absolutelyContinuous_of_le (IicSnd_le_fst Ï r)

theorem IsFiniteMeasure.IicSnd {Ï : Measure (Î± Ã— â„)} [IsFiniteMeasure Ï] (r : â„) :
    IsFiniteMeasure (Ï.IicSnd r) :=
  isFiniteMeasure_of_le _ (IicSnd_le_fst Ï _)

theorem iInf_IicSnd_gt (t : â„š) {s : Set Î±} (hs : MeasurableSet s) [IsFiniteMeasure Ï] :
    â¨… r : { r' : â„š // t < r' }, Ï.IicSnd r s = Ï.IicSnd t s := by
  simp_rw [Ï.IicSnd_apply _ hs, Measure.iInf_rat_gt_prod_Iic hs]

theorem tendsto_IicSnd_atTop {s : Set Î±} (hs : MeasurableSet s) :
    Tendsto (fun r : â„š â†¦ Ï.IicSnd r s) atTop (ğ“ (Ï.fst s)) := by
  simp_rw [Ï.IicSnd_apply _ hs, fst_apply hs, â† prod_univ]
  rw [â† Real.iUnion_Iic_rat, prod_iUnion]
  apply tendsto_measure_iUnion_atTop
  exact monotone_const.set_prod Rat.cast_mono.Iic

theorem tendsto_IicSnd_atBot [IsFiniteMeasure Ï] {s : Set Î±} (hs : MeasurableSet s) :
    Tendsto (fun r : â„š â†¦ Ï.IicSnd r s) atBot (ğ“ 0) := by
  simp_rw [Ï.IicSnd_apply _ hs]
  have h_empty : Ï (s Ã—Ë¢ âˆ…) = 0 := by simp only [prod_empty, measure_empty]
  rw [â† h_empty, â† Real.iInter_Iic_rat, prod_iInter]
  suffices h_neg :
      Tendsto (fun r : â„š â†¦ Ï (s Ã—Ë¢ Iic â†‘(-r))) atTop (ğ“ (Ï (â‹‚ r : â„š, s Ã—Ë¢ Iic â†‘(-r)))) by
    have h_inter_eq : â‹‚ r : â„š, s Ã—Ë¢ Iic â†‘(-r) = â‹‚ r : â„š, s Ã—Ë¢ Iic (r : â„) := by
      ext1 x
      simp only [mem_iInter, mem_prod, mem_Iic]
      refine âŸ¨fun h i â†¦ âŸ¨(h i).1, ?_âŸ©, fun h i â†¦ âŸ¨(h i).1, ?_âŸ©âŸ© <;> have h' := h (-i)
      Â· rw [neg_neg] at h'; exact h'.2
      Â· exact h'.2
    rw [h_inter_eq] at h_neg
    have h_fun_eq : (fun r : â„š â†¦ Ï (s Ã—Ë¢ Iic (r : â„))) = fun r : â„š â†¦ Ï (s Ã—Ë¢ Iic â†‘(- -r)) := by
      simp_rw [neg_neg]
    rw [h_fun_eq]
    exact h_neg.comp tendsto_neg_atBot_atTop
  refine tendsto_measure_iInter_atTop (fun q â†¦ (hs.prod measurableSet_Iic).nullMeasurableSet)
    ?_ âŸ¨0, measure_ne_top Ï _âŸ©
  refine fun q r hqr â†¦ Set.prod_mono subset_rfl fun x hx â†¦ ?_
  simp only [Rat.cast_neg, mem_Iic] at hx âŠ¢
  refine hx.trans (neg_le_neg ?_)
  exact mod_cast hqr

end MeasureTheory.Measure

open MeasureTheory

namespace ProbabilityTheory

variable {Î± : Type*} {mÎ± : MeasurableSpace Î±}

attribute [local instance] MeasureTheory.Measure.IsFiniteMeasure.IicSnd

/-! ### Auxiliary definitions

We build towards the definition of `ProbabilityTheory.condCDF`. We first define
`ProbabilityTheory.preCDF`, a function defined on `Î± Ã— â„š` with the properties of a cdf almost
everywhere. -/

/-- `preCDF` is the Radon-Nikodym derivative of `Ï.IicSnd` with respect to `Ï.fst` at each
`r : â„š`. This function `â„š â†’ Î± â†’ â„â‰¥0âˆ` is such that for almost all `a : Î±`, the function `â„š â†’ â„â‰¥0âˆ`
satisfies the properties of a cdf (monotone with limit 0 at -âˆ and 1 at +âˆ, right-continuous).

We define this function on `â„š` and not `â„` because `â„š` is countable, which allows us to prove
properties of the form `âˆ€áµ a âˆ‚Ï.fst, âˆ€ q, P (preCDF q a)`, instead of the weaker
`âˆ€ q, âˆ€áµ a âˆ‚Ï.fst, P (preCDF q a)`. -/
noncomputable def preCDF (Ï : Measure (Î± Ã— â„)) (r : â„š) : Î± â†’ â„â‰¥0âˆ :=
  Measure.rnDeriv (Ï.IicSnd r) Ï.fst

theorem measurable_preCDF {Ï : Measure (Î± Ã— â„)} {r : â„š} : Measurable (preCDF Ï r) :=
  Measure.measurable_rnDeriv _ _

lemma measurable_preCDF' {Ï : Measure (Î± Ã— â„)} :
    Measurable fun a r â†¦ (preCDF Ï r a).toReal := by
  rw [measurable_pi_iff]
  exact fun _ â†¦ measurable_preCDF.ennreal_toReal

theorem withDensity_preCDF (Ï : Measure (Î± Ã— â„)) (r : â„š) [IsFiniteMeasure Ï] :
    Ï.fst.withDensity (preCDF Ï r) = Ï.IicSnd r :=
  Measure.absolutelyContinuous_iff_withDensity_rnDeriv_eq.mp (Measure.IicSnd_ac_fst Ï r)

theorem setLIntegral_preCDF_fst (Ï : Measure (Î± Ã— â„)) (r : â„š) {s : Set Î±} (hs : MeasurableSet s)
    [IsFiniteMeasure Ï] : âˆ«â» x in s, preCDF Ï r x âˆ‚Ï.fst = Ï.IicSnd r s := by
  have : âˆ€ r, âˆ«â» x in s, preCDF Ï r x âˆ‚Ï.fst = âˆ«â» x in s, (preCDF Ï r * 1) x âˆ‚Ï.fst := by
    simp only [mul_one, forall_const]
  rw [this, â† setLIntegral_withDensity_eq_setLIntegral_mul _ measurable_preCDF _ hs]
  Â· simp only [withDensity_preCDF Ï r, Pi.one_apply, lintegral_one, Measure.restrict_apply,
      MeasurableSet.univ, univ_inter]
  Â· rw [Pi.one_def]
    exact measurable_const

lemma lintegral_preCDF_fst (Ï : Measure (Î± Ã— â„)) (r : â„š) [IsFiniteMeasure Ï] :
    âˆ«â» x, preCDF Ï r x âˆ‚Ï.fst = Ï.IicSnd r univ := by
  rw [â† setLIntegral_univ, setLIntegral_preCDF_fst Ï r MeasurableSet.univ]

theorem monotone_preCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] :
    âˆ€áµ a âˆ‚Ï.fst, Monotone fun r â†¦ preCDF Ï r a := by
  simp_rw [Monotone, ae_all_iff]
  refine fun r r' hrr' â†¦ ae_le_of_forall_setLIntegral_le_of_sigmaFinite measurable_preCDF
    fun s hs _ â†¦ ?_
  rw [setLIntegral_preCDF_fst Ï r hs, setLIntegral_preCDF_fst Ï r' hs]
  exact Measure.IicSnd_mono Ï (mod_cast hrr') s

theorem preCDF_le_one (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] :
    âˆ€áµ a âˆ‚Ï.fst, âˆ€ r, preCDF Ï r a â‰¤ 1 := by
  rw [ae_all_iff]
  refine fun r â†¦ ae_le_of_forall_setLIntegral_le_of_sigmaFinite measurable_preCDF fun s hs _ â†¦ ?_
  rw [setLIntegral_preCDF_fst Ï r hs]
  simp only [lintegral_one, Measure.restrict_apply, MeasurableSet.univ, univ_inter]
  exact Measure.IicSnd_le_fst Ï r s

lemma setIntegral_preCDF_fst (Ï : Measure (Î± Ã— â„)) (r : â„š) {s : Set Î±} (hs : MeasurableSet s)
    [IsFiniteMeasure Ï] :
    âˆ« x in s, (preCDF Ï r x).toReal âˆ‚Ï.fst = (Ï.IicSnd r).real s := by
  rw [integral_toReal]
  Â· rw [setLIntegral_preCDF_fst _ _ hs, measureReal_def]
  Â· exact measurable_preCDF.aemeasurable
  Â· refine ae_restrict_of_ae ?_
    filter_upwards [preCDF_le_one Ï] with a ha
    exact (ha r).trans_lt ENNReal.one_lt_top

lemma integral_preCDF_fst (Ï : Measure (Î± Ã— â„)) (r : â„š) [IsFiniteMeasure Ï] :
    âˆ« x, (preCDF Ï r x).toReal âˆ‚Ï.fst = (Ï.IicSnd r).real univ := by
  rw [â† setIntegral_univ, setIntegral_preCDF_fst Ï _ MeasurableSet.univ]

lemma integrable_preCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (x : â„š) :
    Integrable (fun a â†¦ (preCDF Ï x a).toReal) Ï.fst := by
  refine integrable_of_forall_fin_meas_le _ (measure_lt_top Ï.fst univ) ?_ fun t _ _ â†¦ ?_
  Â· exact measurable_preCDF.ennreal_toReal.aestronglyMeasurable
  Â· simp_rw [â† ofReal_norm_eq_enorm, Real.norm_of_nonneg ENNReal.toReal_nonneg]
    rw [â† lintegral_one]
    refine (setLIntegral_le_lintegral _ _).trans (lintegral_mono_ae ?_)
    filter_upwards [preCDF_le_one Ï] with a ha using ENNReal.ofReal_toReal_le.trans (ha _)

lemma isRatCondKernelCDFAux_preCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] :
    IsRatCondKernelCDFAux (fun p r â†¦ (preCDF Ï r p.2).toReal)
      (Kernel.const Unit Ï) (Kernel.const Unit Ï.fst) where
  measurable := measurable_preCDF'.comp measurable_snd
  mono' a r r' hrr' := by
    filter_upwards [monotone_preCDF Ï, preCDF_le_one Ï] with a hâ‚ hâ‚‚
    exact ENNReal.toReal_mono ((hâ‚‚ _).trans_lt ENNReal.one_lt_top).ne (hâ‚ hrr')
  nonneg' _ q := by simp
  le_one' a q := by
    simp only [Kernel.const_apply]
    filter_upwards [preCDF_le_one Ï] with a ha
    refine ENNReal.toReal_le_of_le_ofReal zero_le_one ?_
    simp [ha]
  tendsto_integral_of_antitone a s _ hs_tendsto := by
    simp_rw [Kernel.const_apply, integral_preCDF_fst Ï]
    have h := Ï.tendsto_IicSnd_atBot MeasurableSet.univ
    rw [â† ENNReal.toReal_zero]
    have h0 : Tendsto ENNReal.toReal (ğ“ 0) (ğ“ 0) :=
      ENNReal.continuousAt_toReal ENNReal.zero_ne_top
    exact h0.comp (h.comp hs_tendsto)
  tendsto_integral_of_monotone a s _ hs_tendsto := by
    simp_rw [Kernel.const_apply, integral_preCDF_fst Ï]
    have h := Ï.tendsto_IicSnd_atTop MeasurableSet.univ
    have h0 : Tendsto ENNReal.toReal (ğ“ (Ï.fst univ)) (ğ“ (Ï.fst.real univ)) :=
      ENNReal.continuousAt_toReal (measure_ne_top _ _)
    exact h0.comp (h.comp hs_tendsto)
  integrable _ q := integrable_preCDF Ï q
  setIntegral a s hs q := by rw [Kernel.const_apply, Kernel.const_apply,
    setIntegral_preCDF_fst _ _ hs, measureReal_def, measureReal_def, Measure.IicSnd_apply _ _ hs]

lemma isRatCondKernelCDF_preCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] :
    IsRatCondKernelCDF (fun p r â†¦ (preCDF Ï r p.2).toReal)
      (Kernel.const Unit Ï) (Kernel.const Unit Ï.fst) :=
  (isRatCondKernelCDFAux_preCDF Ï).isRatCondKernelCDF

/-! ### Conditional cdf -/

/-- Conditional cdf of the measure given the value on `Î±`, as a Stieltjes function. -/
noncomputable def condCDF (Ï : Measure (Î± Ã— â„)) (a : Î±) : StieltjesFunction :=
  stieltjesOfMeasurableRat (fun a r â†¦ (preCDF Ï r a).toReal) measurable_preCDF' a

lemma condCDF_eq_stieltjesOfMeasurableRat_unit_prod (Ï : Measure (Î± Ã— â„)) (a : Î±) :
    condCDF Ï a = stieltjesOfMeasurableRat (fun (p : Unit Ã— Î±) r â†¦ (preCDF Ï r p.2).toReal)
      (measurable_preCDF'.comp measurable_snd) ((), a) := by
  ext x
  rw [condCDF, â† stieltjesOfMeasurableRat_unit_prod]

lemma isCondKernelCDF_condCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] :
    IsCondKernelCDF (fun p : Unit Ã— Î± â†¦ condCDF Ï p.2) (Kernel.const Unit Ï)
      (Kernel.const Unit Ï.fst) := by
  simp_rw [condCDF_eq_stieltjesOfMeasurableRat_unit_prod Ï]
  exact isCondKernelCDF_stieltjesOfMeasurableRat (isRatCondKernelCDF_preCDF Ï)

/-- The conditional cdf is non-negative for all `a : Î±`. -/
theorem condCDF_nonneg (Ï : Measure (Î± Ã— â„)) (a : Î±) (r : â„) : 0 â‰¤ condCDF Ï a r :=
  stieltjesOfMeasurableRat_nonneg _ a r

/-- The conditional cdf is lower or equal to 1 for all `a : Î±`. -/
theorem condCDF_le_one (Ï : Measure (Î± Ã— â„)) (a : Î±) (x : â„) : condCDF Ï a x â‰¤ 1 :=
  stieltjesOfMeasurableRat_le_one _ _ _

/-- The conditional cdf tends to 0 at -âˆ for all `a : Î±`. -/
theorem tendsto_condCDF_atBot (Ï : Measure (Î± Ã— â„)) (a : Î±) :
    Tendsto (condCDF Ï a) atBot (ğ“ 0) := tendsto_stieltjesOfMeasurableRat_atBot _ _

/-- The conditional cdf tends to 1 at +âˆ for all `a : Î±`. -/
theorem tendsto_condCDF_atTop (Ï : Measure (Î± Ã— â„)) (a : Î±) :
    Tendsto (condCDF Ï a) atTop (ğ“ 1) := tendsto_stieltjesOfMeasurableRat_atTop _ _

theorem condCDF_ae_eq (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (r : â„š) :
    (fun a â†¦ condCDF Ï a r) =áµ[Ï.fst] fun a â†¦ (preCDF Ï r a).toReal := by
  simp_rw [condCDF_eq_stieltjesOfMeasurableRat_unit_prod Ï]
  exact stieltjesOfMeasurableRat_ae_eq (isRatCondKernelCDF_preCDF Ï) () r

theorem ofReal_condCDF_ae_eq (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (r : â„š) :
    (fun a â†¦ ENNReal.ofReal (condCDF Ï a r)) =áµ[Ï.fst] preCDF Ï r := by
  filter_upwards [condCDF_ae_eq Ï r, preCDF_le_one Ï] with a ha ha_le_one
  rw [ha, ENNReal.ofReal_toReal]
  exact ((ha_le_one r).trans_lt ENNReal.one_lt_top).ne

/-- The conditional cdf is a measurable function of `a : Î±` for all `x : â„`. -/
theorem measurable_condCDF (Ï : Measure (Î± Ã— â„)) (x : â„) : Measurable fun a â†¦ condCDF Ï a x :=
  measurable_stieltjesOfMeasurableRat _ _

/-- The conditional cdf is a strongly measurable function of `a : Î±` for all `x : â„`. -/
theorem stronglyMeasurable_condCDF (Ï : Measure (Î± Ã— â„)) (x : â„) :
    StronglyMeasurable fun a â†¦ condCDF Ï a x := stronglyMeasurable_stieltjesOfMeasurableRat _ _

theorem setLIntegral_condCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (x : â„) {s : Set Î±}
    (hs : MeasurableSet s) :
    âˆ«â» a in s, ENNReal.ofReal (condCDF Ï a x) âˆ‚Ï.fst = Ï (s Ã—Ë¢ Iic x) :=
  (isCondKernelCDF_condCDF Ï).setLIntegral () hs x

theorem lintegral_condCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (x : â„) :
    âˆ«â» a, ENNReal.ofReal (condCDF Ï a x) âˆ‚Ï.fst = Ï (univ Ã—Ë¢ Iic x) :=
  (isCondKernelCDF_condCDF Ï).lintegral () x

theorem integrable_condCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (x : â„) :
    Integrable (fun a â†¦ condCDF Ï a x) Ï.fst :=
  (isCondKernelCDF_condCDF Ï).integrable () x

theorem setIntegral_condCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (x : â„) {s : Set Î±}
    (hs : MeasurableSet s) : âˆ« a in s, condCDF Ï a x âˆ‚Ï.fst = Ï.real (s Ã—Ë¢ Iic x) :=
  (isCondKernelCDF_condCDF Ï).setIntegral () hs x

theorem integral_condCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (x : â„) :
    âˆ« a, condCDF Ï a x âˆ‚Ï.fst = Ï.real (univ Ã—Ë¢ Iic x) :=
  (isCondKernelCDF_condCDF Ï).integral () x

section Measure

theorem measure_condCDF_Iic (Ï : Measure (Î± Ã— â„)) (a : Î±) (x : â„) :
    (condCDF Ï a).measure (Iic x) = ENNReal.ofReal (condCDF Ï a x) := by
  rw [â† sub_zero (condCDF Ï a x)]
  exact (condCDF Ï a).measure_Iic (tendsto_condCDF_atBot Ï a) _

theorem measure_condCDF_univ (Ï : Measure (Î± Ã— â„)) (a : Î±) : (condCDF Ï a).measure univ = 1 := by
  rw [â† ENNReal.ofReal_one, â† sub_zero (1 : â„)]
  exact StieltjesFunction.measure_univ _ (tendsto_condCDF_atBot Ï a) (tendsto_condCDF_atTop Ï a)

instance instIsProbabilityMeasureCondCDF (Ï : Measure (Î± Ã— â„)) (a : Î±) :
    IsProbabilityMeasure (condCDF Ï a).measure :=
  âŸ¨measure_condCDF_univ Ï aâŸ©

/-- The function `a â†¦ (condCDF Ï a).measure` is measurable. -/
theorem measurable_measure_condCDF (Ï : Measure (Î± Ã— â„)) :
    Measurable fun a => (condCDF Ï a).measure :=
  .measure_of_isPiSystem_of_isProbabilityMeasure (borel_eq_generateFrom_Iic â„) isPiSystem_Iic <| by
    simp_rw [forall_mem_range, measure_condCDF_Iic]
    exact fun u â†¦ (measurable_condCDF Ï u).ennreal_ofReal

end Measure

end ProbabilityTheory
