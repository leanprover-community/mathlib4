/-
Copyright (c) 2024 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Measure.GiryMonad
import Mathlib.MeasureTheory.Measure.Stieltjes
import Mathlib.Analysis.Normed.Order.Lattice
import Mathlib.MeasureTheory.Function.StronglyMeasurable.Basic

/-!
# Measurable parametric Stieltjes functions

We provide tools to build a measurable function `Î± â†’ StieltjesFunction` with limits 0 at -âˆ and 1 at
+âˆ for all `a : Î±` from a measurable function `f : Î± â†’ â„š â†’ â„`. These measurable parametric Stieltjes
functions are cumulative distribution functions (CDF) of transition kernels.
The reason for going through `â„š` instead of defining directly a Stieltjes function is that since
`â„š` is countable, building a measurable function is easier and we can obtain properties of the
form `âˆ€áµ (a : Î±) âˆ‚Î¼, âˆ€ (q : â„š), ...` (for some measure `Î¼` on `Î±`) by proving the weaker
`âˆ€ (q : â„š), âˆ€áµ (a : Î±) âˆ‚Î¼, ...`.

This construction will be possible if `f a : â„š â†’ â„` satisfies a package of properties for all `a`:
monotonicity, limits at +-âˆ and a continuity property. We define `IsRatStieltjesPoint f a` to state
that this is the case at `a` and define the property `IsMeasurableRatCDF f` that `f` is measurable
and `IsRatStieltjesPoint f a` for all `a`.
The function `Î± â†’ StieltjesFunction` obtained by extending `f` by continuity from the right is then
called `IsMeasurableRatCDF.stieltjesFunction`.

In applications, we will often only have `IsRatStieltjesPoint f a` almost surely with respect to
some measure. In order to turn that almost everywhere property into an everywhere property we define
`toRatCDF (f : Î± â†’ â„š â†’ â„) := fun a q â†¦ if IsRatStieltjesPoint f a then f a q else defaultRatCDF q`,
which satisfies the property `IsMeasurableRatCDF (toRatCDF f)`.

Finally, we define `stieltjesOfMeasurableRat`, composition of `toRatCDF` and
`IsMeasurableRatCDF.stieltjesFunction`.

## Main definitions

* `stieltjesOfMeasurableRat`: turn a measurable function `f : Î± â†’ â„š â†’ â„` into a measurable
  function `Î± â†’ StieltjesFunction`.

-/

open MeasureTheory Set Filter TopologicalSpace

open scoped NNReal ENNReal MeasureTheory Topology

/-- A measurable function `Î± â†’ StieltjesFunction` with limits 0 at -âˆ and 1 at +âˆ gives a measurable
function `Î± â†’ Measure â„` by taking `StieltjesFunction.measure` at each point. -/
lemma StieltjesFunction.measurable_measure {Î± : Type*} {_ : MeasurableSpace Î±}
    {f : Î± â†’ StieltjesFunction} (hf : âˆ€ q, Measurable fun a â†¦ f a q)
    (hf_bot : âˆ€ a, Tendsto (f a) atBot (ğ“ 0))
    (hf_top : âˆ€ a, Tendsto (f a) atTop (ğ“ 1)) :
    Measurable fun a â†¦ (f a).measure :=
  have : âˆ€ a, IsProbabilityMeasure (f a).measure :=
    fun a â†¦ (f a).isProbabilityMeasure (hf_bot a) (hf_top a)
  .measure_of_isPiSystem_of_isProbabilityMeasure (borel_eq_generateFrom_Iic â„) isPiSystem_Iic <| by
    simp_rw [forall_mem_range, StieltjesFunction.measure_Iic (f _) (hf_bot _), sub_zero]
    exact fun _ â†¦ (hf _).ennreal_ofReal

namespace ProbabilityTheory

variable {Î± : Type*}

section IsMeasurableRatCDF

variable {f : Î± â†’ â„š â†’ â„}

/-- `a : Î±` is a Stieltjes point for `f : Î± â†’ â„š â†’ â„` if `f a` is monotone with limit 0 at -âˆ
and 1 at +âˆ and satisfies a continuity property. -/
structure IsRatStieltjesPoint (f : Î± â†’ â„š â†’ â„) (a : Î±) : Prop where
  mono : Monotone (f a)
  tendsto_atTop_one : Tendsto (f a) atTop (ğ“ 1)
  tendsto_atBot_zero : Tendsto (f a) atBot (ğ“ 0)
  iInf_rat_gt_eq : âˆ€ t : â„š, â¨… r : Ioi t, f a r = f a t

lemma isRatStieltjesPoint_unit_prod_iff (f : Î± â†’ â„š â†’ â„) (a : Î±) :
    IsRatStieltjesPoint (fun p : Unit Ã— Î± â†¦ f p.2) ((), a)
      â†” IsRatStieltjesPoint f a := by
  constructor <;>
    exact fun h â†¦ âŸ¨h.mono, h.tendsto_atTop_one, h.tendsto_atBot_zero, h.iInf_rat_gt_eqâŸ©

lemma measurableSet_isRatStieltjesPoint [MeasurableSpace Î±] (hf : Measurable f) :
    MeasurableSet {a | IsRatStieltjesPoint f a} := by
  have h1 : MeasurableSet {a | Monotone (f a)} := by
    change MeasurableSet {a | âˆ€ q r (_ : q â‰¤ r), f a q â‰¤ f a r}
    simp_rw [Set.setOf_forall]
    refine MeasurableSet.iInter (fun q â†¦ ?_)
    refine MeasurableSet.iInter (fun r â†¦ ?_)
    refine MeasurableSet.iInter (fun _ â†¦ ?_)
    exact measurableSet_le hf.eval hf.eval
  have h2 : MeasurableSet {a | Tendsto (f a) atTop (ğ“ 1)} :=
    measurableSet_tendsto _ (fun q â†¦ hf.eval)
  have h3 : MeasurableSet {a | Tendsto (f a) atBot (ğ“ 0)} :=
    measurableSet_tendsto _ (fun q â†¦ hf.eval)
  have h4 : MeasurableSet {a | âˆ€ t : â„š, â¨… r : Ioi t, f a r = f a t} := by
    rw [Set.setOf_forall]
    refine MeasurableSet.iInter (fun q â†¦ ?_)
    exact measurableSet_eq_fun (.iInf fun _ â†¦ hf.eval) hf.eval
  suffices {a | IsRatStieltjesPoint f a}
      = ({a | Monotone (f a)} âˆ© {a | Tendsto (f a) atTop (ğ“ 1)} âˆ© {a | Tendsto (f a) atBot (ğ“ 0)}
        âˆ© {a | âˆ€ t : â„š, â¨… r : Ioi t, f a r = f a t}) by
    rw [this]
    exact (((h1.inter h2).inter h3).inter h4)
  ext a
  simp only [mem_setOf_eq, mem_inter_iff]
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· exact âŸ¨âŸ¨âŸ¨h.mono, h.tendsto_atTop_oneâŸ©, h.tendsto_atBot_zeroâŸ©, h.iInf_rat_gt_eqâŸ©
  Â· exact âŸ¨h.1.1.1, h.1.1.2, h.1.2, h.2âŸ©

lemma IsRatStieltjesPoint.ite {f g : Î± â†’ â„š â†’ â„} {a : Î±} (p : Î± â†’ Prop) [DecidablePred p]
    (hf : p a â†’ IsRatStieltjesPoint f a) (hg : Â¬ p a â†’ IsRatStieltjesPoint g a) :
    IsRatStieltjesPoint (fun a â†¦ if p a then f a else g a) a where
  mono := by split_ifs with h; exacts [(hf h).mono, (hg h).mono]
  tendsto_atTop_one := by
    split_ifs with h; exacts [(hf h).tendsto_atTop_one, (hg h).tendsto_atTop_one]
  tendsto_atBot_zero := by
    split_ifs with h; exacts [(hf h).tendsto_atBot_zero, (hg h).tendsto_atBot_zero]
  iInf_rat_gt_eq := by split_ifs with h; exacts [(hf h).iInf_rat_gt_eq, (hg h).iInf_rat_gt_eq]

variable [MeasurableSpace Î±]

/-- A function `f : Î± â†’ â„š â†’ â„` is a (kernel) rational cumulative distribution function if it is
measurable in the first argument and if `f a` satisfies a list of properties for all `a : Î±`:
monotonicity between 0 at -âˆ and 1 at +âˆ and a form of continuity.

A function with these properties can be extended to a measurable function `Î± â†’ StieltjesFunction`.
See `ProbabilityTheory.IsMeasurableRatCDF.stieltjesFunction`.
-/
structure IsMeasurableRatCDF (f : Î± â†’ â„š â†’ â„) : Prop where
  isRatStieltjesPoint : âˆ€ a, IsRatStieltjesPoint f a
  measurable : Measurable f

lemma IsMeasurableRatCDF.nonneg {f : Î± â†’ â„š â†’ â„} (hf : IsMeasurableRatCDF f) (a : Î±) (q : â„š) :
    0 â‰¤ f a q :=
  Monotone.le_of_tendsto (hf.isRatStieltjesPoint a).mono
    (hf.isRatStieltjesPoint a).tendsto_atBot_zero q

lemma IsMeasurableRatCDF.le_one {f : Î± â†’ â„š â†’ â„} (hf : IsMeasurableRatCDF f) (a : Î±) (q : â„š) :
    f a q â‰¤ 1 :=
  Monotone.ge_of_tendsto (hf.isRatStieltjesPoint a).mono
    (hf.isRatStieltjesPoint a).tendsto_atTop_one q

lemma IsMeasurableRatCDF.tendsto_atTop_one {f : Î± â†’ â„š â†’ â„} (hf : IsMeasurableRatCDF f) (a : Î±) :
    Tendsto (f a) atTop (ğ“ 1) := (hf.isRatStieltjesPoint a).tendsto_atTop_one

lemma IsMeasurableRatCDF.tendsto_atBot_zero {f : Î± â†’ â„š â†’ â„} (hf : IsMeasurableRatCDF f) (a : Î±) :
    Tendsto (f a) atBot (ğ“ 0) := (hf.isRatStieltjesPoint a).tendsto_atBot_zero

lemma IsMeasurableRatCDF.iInf_rat_gt_eq {f : Î± â†’ â„š â†’ â„} (hf : IsMeasurableRatCDF f) (a : Î±)
    (q : â„š) :
    â¨… r : Ioi q, f a r = f a q := (hf.isRatStieltjesPoint a).iInf_rat_gt_eq q

end IsMeasurableRatCDF

section DefaultRatCDF

/-- A function with the property `IsMeasurableRatCDF`.
Used in a piecewise construction to convert a function which only satisfies the properties
defining `IsMeasurableRatCDF` on some set into a true `IsMeasurableRatCDF`. -/
def defaultRatCDF (q : â„š) := if q < 0 then (0 : â„) else 1

lemma monotone_defaultRatCDF : Monotone defaultRatCDF := by
  unfold defaultRatCDF
  intro x y hxy
  dsimp only
  split_ifs with h_1 h_2 h_2
  exacts [le_rfl, zero_le_one, absurd (hxy.trans_lt h_2) h_1, le_rfl]

lemma defaultRatCDF_nonneg (q : â„š) : 0 â‰¤ defaultRatCDF q := by
  unfold defaultRatCDF
  split_ifs
  exacts [le_rfl, zero_le_one]

lemma defaultRatCDF_le_one (q : â„š) : defaultRatCDF q â‰¤ 1 := by
  unfold defaultRatCDF
  split_ifs <;> simp

lemma tendsto_defaultRatCDF_atTop : Tendsto defaultRatCDF atTop (ğ“ 1) := by
  refine (tendsto_congr' ?_).mp tendsto_const_nhds
  rw [EventuallyEq, eventually_atTop]
  exact âŸ¨0, fun q hq => (if_neg (not_lt.mpr hq)).symmâŸ©

lemma tendsto_defaultRatCDF_atBot : Tendsto defaultRatCDF atBot (ğ“ 0) := by
  refine (tendsto_congr' ?_).mp tendsto_const_nhds
  rw [EventuallyEq, eventually_atBot]
  refine âŸ¨-1, fun q hq => (if_pos (hq.trans_lt ?_)).symmâŸ©
  linarith

lemma iInf_rat_gt_defaultRatCDF (t : â„š) :
    â¨… r : Ioi t, defaultRatCDF r = defaultRatCDF t := by
  simp only [defaultRatCDF]
  have h_bdd : BddBelow (range fun r : â†¥(Ioi t) â†¦ ite ((r : â„š) < 0) (0 : â„) 1) := by
    refine âŸ¨0, fun x hx â†¦ ?_âŸ©
    obtain âŸ¨y, rflâŸ© := mem_range.mpr hx
    dsimp only
    split_ifs
    exacts [le_rfl, zero_le_one]
  split_ifs with h
  Â· refine le_antisymm ?_ (le_ciInf fun x â†¦ ?_)
    Â· obtain âŸ¨q, htq, hq_negâŸ© : âˆƒ q, t < q âˆ§ q < 0 := âŸ¨t / 2, by linarith, by linarithâŸ©
      refine (ciInf_le h_bdd âŸ¨q, htqâŸ©).trans ?_
      rw [if_pos]
      rwa [Subtype.coe_mk]
    Â· split_ifs
      exacts [le_rfl, zero_le_one]
  Â· refine le_antisymm ?_ ?_
    Â· refine (ciInf_le h_bdd âŸ¨t + 1, lt_add_one tâŸ©).trans ?_
      split_ifs
      exacts [zero_le_one, le_rfl]
    Â· refine le_ciInf fun x â†¦ ?_
      rw [if_neg]
      rw [not_lt] at h âŠ¢
      exact h.trans (mem_Ioi.mp x.prop).le

lemma isRatStieltjesPoint_defaultRatCDF (a : Î±) :
    IsRatStieltjesPoint (fun (_ : Î±) â†¦ defaultRatCDF) a where
  mono := monotone_defaultRatCDF
  tendsto_atTop_one := tendsto_defaultRatCDF_atTop
  tendsto_atBot_zero := tendsto_defaultRatCDF_atBot
  iInf_rat_gt_eq := iInf_rat_gt_defaultRatCDF

lemma IsMeasurableRatCDF_defaultRatCDF (Î± : Type*) [MeasurableSpace Î±] :
    IsMeasurableRatCDF (fun (_ : Î±) (q : â„š) â†¦ defaultRatCDF q) where
  isRatStieltjesPoint := isRatStieltjesPoint_defaultRatCDF
  measurable := measurable_const

end DefaultRatCDF

section ToRatCDF

variable {f : Î± â†’ â„š â†’ â„}

open scoped Classical in
/-- Turn a function `f : Î± â†’ â„š â†’ â„` into another with the property `IsRatStieltjesPoint f a`
everywhere. At `a` that does not satisfy that property, `f a` is replaced by an arbitrary suitable
function.
Mainly useful when `f` satisfies the property `IsRatStieltjesPoint f a` almost everywhere with
respect to some measure. -/
noncomputable
def toRatCDF (f : Î± â†’ â„š â†’ â„) : Î± â†’ â„š â†’ â„ := fun a â†¦
  if IsRatStieltjesPoint f a then f a else defaultRatCDF

lemma toRatCDF_of_isRatStieltjesPoint {a : Î±} (h : IsRatStieltjesPoint f a) (q : â„š) :
    toRatCDF f a q = f a q := by
  rw [toRatCDF, if_pos h]

lemma toRatCDF_unit_prod (a : Î±) :
    toRatCDF (fun (p : Unit Ã— Î±) â†¦ f p.2) ((), a) = toRatCDF f a := by
  unfold toRatCDF
  rw [isRatStieltjesPoint_unit_prod_iff]

variable [MeasurableSpace Î±]

lemma measurable_toRatCDF (hf : Measurable f) : Measurable (toRatCDF f) :=
  Measurable.ite (measurableSet_isRatStieltjesPoint hf) hf measurable_const

lemma isMeasurableRatCDF_toRatCDF (hf : Measurable f) :
    IsMeasurableRatCDF (toRatCDF f) where
  isRatStieltjesPoint a := by
    classical
    exact IsRatStieltjesPoint.ite (IsRatStieltjesPoint f) id
      (fun _ â†¦ isRatStieltjesPoint_defaultRatCDF a)
  measurable := measurable_toRatCDF hf

end ToRatCDF

section IsMeasurableRatCDF.stieltjesFunction

/-- Auxiliary definition for `IsMeasurableRatCDF.stieltjesFunction`: turn `f : Î± â†’ â„š â†’ â„` into
a function `Î± â†’ â„ â†’ â„` by assigning to `f a x` the infimum of `f a q` over `q : â„š` with `x < q`. -/
noncomputable irreducible_def IsMeasurableRatCDF.stieltjesFunctionAux (f : Î± â†’ â„š â†’ â„) :
    Î± â†’ â„ â†’ â„ :=
  fun a x â†¦ â¨… q : { q' : â„š // x < q' }, f a q

lemma IsMeasurableRatCDF.stieltjesFunctionAux_def' (f : Î± â†’ â„š â†’ â„) (a : Î±) :
    IsMeasurableRatCDF.stieltjesFunctionAux f a
      = fun (t : â„) â†¦ â¨… r : { r' : â„š // t < r' }, f a r := by
  ext t; exact IsMeasurableRatCDF.stieltjesFunctionAux_def f a t

lemma IsMeasurableRatCDF.stieltjesFunctionAux_unit_prod {f : Î± â†’ â„š â†’ â„} (a : Î±) :
    IsMeasurableRatCDF.stieltjesFunctionAux (fun (p : Unit Ã— Î±) â†¦ f p.2) ((), a)
      = IsMeasurableRatCDF.stieltjesFunctionAux f a := by
  simp_rw [IsMeasurableRatCDF.stieltjesFunctionAux_def']

variable {f : Î± â†’ â„š â†’ â„} [MeasurableSpace Î±] (hf : IsMeasurableRatCDF f)
include hf

lemma IsMeasurableRatCDF.stieltjesFunctionAux_eq (a : Î±) (r : â„š) :
    IsMeasurableRatCDF.stieltjesFunctionAux f a r = f a r := by
  rw [â† hf.iInf_rat_gt_eq a r, IsMeasurableRatCDF.stieltjesFunctionAux]
  refine Equiv.iInf_congr ?_ ?_
  Â· exact
      { toFun := fun t â†¦ âŸ¨t.1, mod_cast t.2âŸ©
        invFun := fun t â†¦ âŸ¨t.1, mod_cast t.2âŸ©
        left_inv := fun t â†¦ by simp only [Subtype.coe_eta]
        right_inv := fun t â†¦ by simp only [Subtype.coe_eta] }
  Â· intro t
    simp only [Equiv.coe_fn_mk, Subtype.coe_mk]

lemma IsMeasurableRatCDF.stieltjesFunctionAux_nonneg (a : Î±) (r : â„) :
    0 â‰¤ IsMeasurableRatCDF.stieltjesFunctionAux f a r := by
  have : Nonempty { r' : â„š // r < â†‘r' } := by
    obtain âŸ¨r, hrxâŸ© := exists_rat_gt r
    exact âŸ¨âŸ¨r, hrxâŸ©âŸ©
  rw [IsMeasurableRatCDF.stieltjesFunctionAux_def]
  exact le_ciInf fun r' â†¦ hf.nonneg a _

lemma IsMeasurableRatCDF.monotone_stieltjesFunctionAux (a : Î±) :
    Monotone (IsMeasurableRatCDF.stieltjesFunctionAux f a) := by
  intro x y hxy
  have : Nonempty { r' : â„š // y < â†‘r' } := by
    obtain âŸ¨r, hrxâŸ© := exists_rat_gt y
    exact âŸ¨âŸ¨r, hrxâŸ©âŸ©
  simp_rw [IsMeasurableRatCDF.stieltjesFunctionAux_def]
  refine le_ciInf fun r â†¦ (ciInf_le ?_ ?_).trans_eq ?_
  Â· refine âŸ¨0, fun z â†¦ ?_âŸ©; rintro âŸ¨u, rflâŸ©; exact hf.nonneg a _
  Â· exact âŸ¨r.1, hxy.trans_lt r.propâŸ©
  Â· rfl

lemma IsMeasurableRatCDF.continuousWithinAt_stieltjesFunctionAux_Ici (a : Î±) (x : â„) :
    ContinuousWithinAt (IsMeasurableRatCDF.stieltjesFunctionAux f a) (Ici x) x := by
  rw [â† continuousWithinAt_Ioi_iff_Ici]
  convert Monotone.tendsto_nhdsGT (monotone_stieltjesFunctionAux hf a) x
  rw [sInf_image']
  have h' : â¨… r : Ioi x, stieltjesFunctionAux f a r
      = â¨… r : { r' : â„š // x < r' }, stieltjesFunctionAux f a r := by
    refine Real.iInf_Ioi_eq_iInf_rat_gt x ?_ (monotone_stieltjesFunctionAux hf a)
    refine âŸ¨0, fun z â†¦ ?_âŸ©
    rintro âŸ¨u, -, rflâŸ©
    exact stieltjesFunctionAux_nonneg hf a u
  have h'' :
    â¨… r : { r' : â„š // x < r' }, stieltjesFunctionAux f a r =
      â¨… r : { r' : â„š // x < r' }, f a r := by
    congr with r
    exact stieltjesFunctionAux_eq hf a r
  rw [h', h'', ContinuousWithinAt]
  congr!
  rw [stieltjesFunctionAux_def]

/-- Extend a function `f : Î± â†’ â„š â†’ â„` with property `IsMeasurableRatCDF` from `â„š` to `â„`,
to a function `Î± â†’ StieltjesFunction`. -/
noncomputable def IsMeasurableRatCDF.stieltjesFunction (a : Î±) : StieltjesFunction where
  toFun := stieltjesFunctionAux f a
  mono' := monotone_stieltjesFunctionAux hf a
  right_continuous' x := continuousWithinAt_stieltjesFunctionAux_Ici hf a x

lemma IsMeasurableRatCDF.stieltjesFunction_eq (a : Î±) (r : â„š) : hf.stieltjesFunction a r = f a r :=
  stieltjesFunctionAux_eq hf a r

lemma IsMeasurableRatCDF.stieltjesFunction_nonneg (a : Î±) (r : â„) : 0 â‰¤ hf.stieltjesFunction a r :=
  stieltjesFunctionAux_nonneg hf a r

lemma IsMeasurableRatCDF.stieltjesFunction_le_one (a : Î±) (x : â„) :
    hf.stieltjesFunction a x â‰¤ 1 := by
  obtain âŸ¨r, hrxâŸ© := exists_rat_gt x
  rw [â† StieltjesFunction.iInf_rat_gt_eq]
  simp_rw [IsMeasurableRatCDF.stieltjesFunction_eq]
  refine ciInf_le_of_le ?_ ?_ (hf.le_one _ _)
  Â· refine âŸ¨0, fun z â†¦ ?_âŸ©; rintro âŸ¨u, rflâŸ©; exact hf.nonneg a _
  Â· exact âŸ¨r, hrxâŸ©

lemma IsMeasurableRatCDF.tendsto_stieltjesFunction_atBot (a : Î±) :
    Tendsto (hf.stieltjesFunction a) atBot (ğ“ 0) := by
  have h_exists : âˆ€ x : â„, âˆƒ q : â„š, x < q âˆ§ â†‘q < x + 1 := fun x â†¦ exists_rat_btwn (lt_add_one x)
  let qs : â„ â†’ â„š := fun x â†¦ (h_exists x).choose
  have hqs_tendsto : Tendsto qs atBot atBot := by
    rw [tendsto_atBot_atBot]
    refine fun q â†¦ âŸ¨q - 1, fun y hy â†¦ ?_âŸ©
    have h_le : â†‘(qs y) â‰¤ (q : â„) - 1 + 1 :=
      (h_exists y).choose_spec.2.le.trans (add_le_add hy le_rfl)
    rw [sub_add_cancel] at h_le
    exact mod_cast h_le
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds
    ((hf.tendsto_atBot_zero a).comp hqs_tendsto) (stieltjesFunction_nonneg hf a) fun x â†¦ ?_
  rw [Function.comp_apply, â† stieltjesFunction_eq hf]
  exact (hf.stieltjesFunction a).mono (h_exists x).choose_spec.1.le

lemma IsMeasurableRatCDF.tendsto_stieltjesFunction_atTop (a : Î±) :
    Tendsto (hf.stieltjesFunction a) atTop (ğ“ 1) := by
  have h_exists : âˆ€ x : â„, âˆƒ q : â„š, x - 1 < q âˆ§ â†‘q < x := fun x â†¦ exists_rat_btwn (sub_one_lt x)
  let qs : â„ â†’ â„š := fun x â†¦ (h_exists x).choose
  have hqs_tendsto : Tendsto qs atTop atTop := by
    rw [tendsto_atTop_atTop]
    refine fun q â†¦ âŸ¨q + 1, fun y hy â†¦ ?_âŸ©
    have h_le : y - 1 â‰¤ qs y := (h_exists y).choose_spec.1.le
    rw [sub_le_iff_le_add] at h_le
    exact_mod_cast le_of_add_le_add_right (hy.trans h_le)
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le ((hf.tendsto_atTop_one a).comp hqs_tendsto)
      tendsto_const_nhds ?_ (stieltjesFunction_le_one hf a)
  intro x
  rw [Function.comp_apply, â† stieltjesFunction_eq hf]
  exact (hf.stieltjesFunction a).mono (le_of_lt (h_exists x).choose_spec.2)

lemma IsMeasurableRatCDF.measurable_stieltjesFunction (x : â„) :
    Measurable fun a â†¦ hf.stieltjesFunction a x := by
  have : (fun a â†¦ hf.stieltjesFunction a x) = fun a â†¦ â¨… r : { r' : â„š // x < r' }, f a â†‘r := by
    ext1 a
    rw [â† StieltjesFunction.iInf_rat_gt_eq]
    congr with q
    rw [stieltjesFunction_eq]
  rw [this]
  exact .iInf (fun q â†¦ hf.measurable.eval)

lemma IsMeasurableRatCDF.stronglyMeasurable_stieltjesFunction (x : â„) :
    StronglyMeasurable fun a â†¦ hf.stieltjesFunction a x :=
  (measurable_stieltjesFunction hf x).stronglyMeasurable

section Measure

lemma IsMeasurableRatCDF.measure_stieltjesFunction_Iic (a : Î±) (x : â„) :
    (hf.stieltjesFunction a).measure (Iic x) = ENNReal.ofReal (hf.stieltjesFunction a x) := by
  rw [â† sub_zero (hf.stieltjesFunction a x)]
  exact (hf.stieltjesFunction a).measure_Iic (tendsto_stieltjesFunction_atBot hf a) _

lemma IsMeasurableRatCDF.measure_stieltjesFunction_univ (a : Î±) :
    (hf.stieltjesFunction a).measure univ = 1 := by
  rw [â† ENNReal.ofReal_one, â† sub_zero (1 : â„)]
  exact StieltjesFunction.measure_univ _ (tendsto_stieltjesFunction_atBot hf a)
    (tendsto_stieltjesFunction_atTop hf a)

instance IsMeasurableRatCDF.instIsProbabilityMeasure_stieltjesFunction (a : Î±) :
    IsProbabilityMeasure (hf.stieltjesFunction a).measure :=
  âŸ¨measure_stieltjesFunction_univ hf aâŸ©

lemma IsMeasurableRatCDF.measurable_measure_stieltjesFunction :
    Measurable fun a â†¦ (hf.stieltjesFunction a).measure := by
  apply_rules [StieltjesFunction.measurable_measure, measurable_stieltjesFunction,
    tendsto_stieltjesFunction_atBot, tendsto_stieltjesFunction_atTop]

end Measure

end IsMeasurableRatCDF.stieltjesFunction

section stieltjesOfMeasurableRat

variable {f : Î± â†’ â„š â†’ â„} [MeasurableSpace Î±]

/-- Turn a measurable function `f : Î± â†’ â„š â†’ â„` into a measurable function `Î± â†’ StieltjesFunction`.
Composition of `toRatCDF` and `IsMeasurableRatCDF.stieltjesFunction`. -/
noncomputable
def stieltjesOfMeasurableRat (f : Î± â†’ â„š â†’ â„) (hf : Measurable f) : Î± â†’ StieltjesFunction :=
  (isMeasurableRatCDF_toRatCDF hf).stieltjesFunction

lemma stieltjesOfMeasurableRat_eq (hf : Measurable f) (a : Î±) (r : â„š) :
    stieltjesOfMeasurableRat f hf a r = toRatCDF f a r :=
  IsMeasurableRatCDF.stieltjesFunction_eq _ a r

lemma stieltjesOfMeasurableRat_unit_prod (hf : Measurable f) (a : Î±) :
    stieltjesOfMeasurableRat (fun (p : Unit Ã— Î±) â†¦ f p.2) (hf.comp measurable_snd) ((), a)
      = stieltjesOfMeasurableRat f hf a := by
  simp_rw [stieltjesOfMeasurableRat, IsMeasurableRatCDF.stieltjesFunction,
    â† IsMeasurableRatCDF.stieltjesFunctionAux_unit_prod a]
  congr 1 with x
  congr 1 with p : 1
  cases p with
  | mk _ b => rw [â† toRatCDF_unit_prod b]

lemma stieltjesOfMeasurableRat_nonneg (hf : Measurable f) (a : Î±) (r : â„) :
    0 â‰¤ stieltjesOfMeasurableRat f hf a r := IsMeasurableRatCDF.stieltjesFunction_nonneg _ a r

lemma stieltjesOfMeasurableRat_le_one (hf : Measurable f) (a : Î±) (x : â„) :
    stieltjesOfMeasurableRat f hf a x â‰¤ 1 := IsMeasurableRatCDF.stieltjesFunction_le_one _ a x

lemma tendsto_stieltjesOfMeasurableRat_atBot (hf : Measurable f) (a : Î±) :
    Tendsto (stieltjesOfMeasurableRat f hf a) atBot (ğ“ 0) :=
  IsMeasurableRatCDF.tendsto_stieltjesFunction_atBot _ a

lemma tendsto_stieltjesOfMeasurableRat_atTop (hf : Measurable f) (a : Î±) :
    Tendsto (stieltjesOfMeasurableRat f hf a) atTop (ğ“ 1) :=
  IsMeasurableRatCDF.tendsto_stieltjesFunction_atTop _ a

lemma measurable_stieltjesOfMeasurableRat (hf : Measurable f) (x : â„) :
    Measurable fun a â†¦ stieltjesOfMeasurableRat f hf a x :=
  IsMeasurableRatCDF.measurable_stieltjesFunction _ x

lemma stronglyMeasurable_stieltjesOfMeasurableRat (hf : Measurable f) (x : â„) :
    StronglyMeasurable fun a â†¦ stieltjesOfMeasurableRat f hf a x :=
  IsMeasurableRatCDF.stronglyMeasurable_stieltjesFunction _ x

section Measure

lemma measure_stieltjesOfMeasurableRat_Iic (hf : Measurable f) (a : Î±) (x : â„) :
    (stieltjesOfMeasurableRat f hf a).measure (Iic x)
      = ENNReal.ofReal (stieltjesOfMeasurableRat f hf a x) :=
  IsMeasurableRatCDF.measure_stieltjesFunction_Iic _ _ _

lemma measure_stieltjesOfMeasurableRat_univ (hf : Measurable f) (a : Î±) :
    (stieltjesOfMeasurableRat f hf a).measure univ = 1 :=
  IsMeasurableRatCDF.measure_stieltjesFunction_univ _ _

instance instIsProbabilityMeasure_stieltjesOfMeasurableRat
    (hf : Measurable f) (a : Î±) :
    IsProbabilityMeasure (stieltjesOfMeasurableRat f hf a).measure :=
  IsMeasurableRatCDF.instIsProbabilityMeasure_stieltjesFunction _ _

lemma measurable_measure_stieltjesOfMeasurableRat (hf : Measurable f) :
    Measurable fun a â†¦ (stieltjesOfMeasurableRat f hf a).measure :=
  IsMeasurableRatCDF.measurable_measure_stieltjesFunction _

end Measure

end stieltjesOfMeasurableRat

end ProbabilityTheory
