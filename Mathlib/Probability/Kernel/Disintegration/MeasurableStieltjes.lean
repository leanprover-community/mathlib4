/-
Copyright (c) 2024 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Data.Complex.Abs
import Mathlib.MeasureTheory.Constructions.Polish
import Mathlib.MeasureTheory.Measure.GiryMonad
import Mathlib.MeasureTheory.Measure.Stieltjes
import Mathlib.Analysis.Normed.Order.Lattice
import Mathlib.MeasureTheory.Function.StronglyMeasurable.Basic
import Mathlib.Probability.Kernel.Disintegration.AuxLemmas

/-!
# Cumulative distributions functions of Markov kernels

-/


open MeasureTheory Set Filter TopologicalSpace

open scoped NNReal ENNReal MeasureTheory Topology

namespace ProbabilityTheory

variable {Î± Î² Î¹ : Type*} [MeasurableSpace Î±]

section IsCDFLike

variable {f : Î± â†’ â„š â†’ â„}

structure IsRatStieltjesPoint (f : Î± â†’ â„š â†’ â„) (a : Î±) : Prop where
  mono : Monotone (f a)
  nonneg : âˆ€ q, 0 â‰¤ f a q
  le_one : âˆ€ q, f a q â‰¤ 1
  tendsto_atTop_one : Tendsto (f a) atTop (ğ“ 1)
  tendsto_atBot_zero : Tendsto (f a) atBot (ğ“ 0)
  iInf_rat_gt_eq : âˆ€ t : â„š, â¨… r : Ioi t, f a r = f a t

lemma isRatStieltjesPoint_unit_prod_iff (f : Î± â†’ â„š â†’ â„) (a : Î±) :
    IsRatStieltjesPoint (fun p : Unit Ã— Î± â†¦ f p.2) ((), a)
      â†” IsRatStieltjesPoint f a := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· exact âŸ¨h.mono, h.nonneg, h.le_one, h.tendsto_atTop_one, h.tendsto_atBot_zero,
      h.iInf_rat_gt_eqâŸ©
  Â· exact âŸ¨h.mono, h.nonneg, h.le_one, h.tendsto_atTop_one, h.tendsto_atBot_zero,
      h.iInf_rat_gt_eqâŸ©

lemma measurableSet_isRatStieltjesPoint (hf : âˆ€ q, Measurable (fun a â†¦ f a q)) :
    MeasurableSet {a | IsRatStieltjesPoint f a} := by
  have h1 : MeasurableSet {a | Monotone (f a)} := by
    change MeasurableSet {a | âˆ€ q r (_ : q â‰¤ r), f a q â‰¤ f a r}
    simp_rw [Set.setOf_forall]
    refine MeasurableSet.iInter (fun q â†¦ ?_)
    refine MeasurableSet.iInter (fun r â†¦ ?_)
    refine MeasurableSet.iInter (fun _ â†¦ ?_)
    exact measurableSet_le (hf q) (hf r)
  have h2 : MeasurableSet {a | âˆ€ q, 0 â‰¤ f a q} := by
    simp_rw [Set.setOf_forall]
    exact MeasurableSet.iInter (fun q â†¦ measurableSet_le measurable_const (hf q))
  have h3 : MeasurableSet {a | âˆ€ q, f a q â‰¤ 1} := by
    simp_rw [Set.setOf_forall]
    exact MeasurableSet.iInter (fun q â†¦ measurableSet_le (hf q) measurable_const)
  have h4 : MeasurableSet {a | Tendsto (f a) atTop (ğ“ 1)} :=
    measurableSet_tendsto_nhds (fun q â†¦ hf q) 1
  have h5 : MeasurableSet {a | Tendsto (f a) atBot (ğ“ 0)} :=
    measurableSet_tendsto_nhds (fun q â†¦ hf q) 0
  have h6 : MeasurableSet {a | âˆ€ t : â„š, â¨… r : Ioi t, f a r = f a t} := by
    rw [Set.setOf_forall]
    refine MeasurableSet.iInter (fun q â†¦ ?_)
    exact measurableSet_eq_fun (measurable_iInf fun _ â†¦ hf _) (hf _)
  suffices {a | IsRatStieltjesPoint f a}
      = ({a | Monotone (f a)} âˆ© {a | âˆ€ (q : â„š), 0 â‰¤ f a q} âˆ© {a | âˆ€ (q : â„š), f a q â‰¤ 1}
        âˆ© {a | Tendsto (f a) atTop (ğ“ 1)} âˆ© {a | Tendsto (f a) atBot (ğ“ 0)} âˆ©
        {a | âˆ€ t : â„š, â¨… r : Ioi t, f a r = f a t}) by
    rw [this]
    exact ((((h1.inter h2).inter h3).inter h4).inter h5).inter h6
  ext a
  simp only [mem_setOf_eq, mem_inter_iff]
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· exact âŸ¨âŸ¨âŸ¨âŸ¨âŸ¨h.mono, h.nonnegâŸ©, h.le_oneâŸ©, h.tendsto_atTop_oneâŸ©, h.tendsto_atBot_zeroâŸ©,
      h.iInf_rat_gt_eqâŸ©
  Â· exact âŸ¨h.1.1.1.1.1, h.1.1.1.1.2, h.1.1.1.2, h.1.1.2, h.1.2, h.2âŸ©

structure IsCDFLike (f : Î± â†’ â„š â†’ â„) : Prop where
  mono : âˆ€ a, Monotone (f a)
  nonneg : âˆ€ a q, 0 â‰¤ f a q
  le_one : âˆ€ a q, f a q â‰¤ 1
  tendsto_atTop_one : âˆ€ a, Tendsto (f a) atTop (ğ“ 1)
  tendsto_atBot_zero : âˆ€ a, Tendsto (f a) atBot (ğ“ 0)
  iInf_rat_gt_eq : âˆ€ a, âˆ€ t : â„š, â¨… r : Ioi t, f a r = f a t
  measurable : âˆ€ q, Measurable (fun a â†¦ f a q)

end IsCDFLike

section DefaultRatCDF

def defaultRatCDF (q : â„š) := if q < 0 then (0 : â„) else 1

lemma monotone_defaultRatCDF : Monotone defaultRatCDF := by
  unfold defaultRatCDF
  intro x y hxy
  dsimp only
  split_ifs with h_1 h_2 h_2
  exacts [le_rfl, zero_le_one, absurd (hxy.trans_lt h_2) h_1, le_rfl]

lemma defaultRatCDF_nonneg (q : â„š) : 0 â‰¤ defaultRatCDF q := by
  unfold defaultRatCDF
  split_ifs
  exacts [le_rfl, zero_le_one]

lemma defaultRatCDF_le_one (q : â„š) : defaultRatCDF q â‰¤ 1 := by
  unfold defaultRatCDF
  split_ifs <;> simp

lemma tendsto_defaultRatCDF_atTop : Tendsto defaultRatCDF atTop (ğ“ 1) := by
  refine (tendsto_congr' ?_).mp tendsto_const_nhds
  rw [EventuallyEq, eventually_atTop]
  exact âŸ¨0, fun q hq => (if_neg (not_lt.mpr hq)).symmâŸ©

lemma tendsto_defaultRatCDF_atBot : Tendsto defaultRatCDF atBot (ğ“ 0) := by
  refine (tendsto_congr' ?_).mp tendsto_const_nhds
  rw [EventuallyEq, eventually_atBot]
  refine âŸ¨-1, fun q hq => (if_pos (hq.trans_lt ?_)).symmâŸ©
  linarith

lemma inf_gt_rat_defaultRatCDF (t : â„š) :
    â¨… r : Ioi t, defaultRatCDF r = defaultRatCDF t := by
  simp only [defaultRatCDF]
  have h_bdd : BddBelow (range fun r : â†¥(Ioi t) â†¦ ite ((r : â„š) < 0) (0 : â„) 1) := by
    refine âŸ¨0, fun x hx â†¦ ?_âŸ©
    obtain âŸ¨y, rflâŸ© := mem_range.mpr hx
    dsimp only
    split_ifs
    exacts [le_rfl, zero_le_one]
  split_ifs with h
  Â· refine le_antisymm ?_ (le_ciInf fun x â†¦ ?_)
    Â· obtain âŸ¨q, htq, hq_negâŸ© : âˆƒ q, t < q âˆ§ q < 0 := âŸ¨t / 2, by linarith, by linarithâŸ©
      refine (ciInf_le h_bdd âŸ¨q, htqâŸ©).trans ?_
      rw [if_pos]
      rwa [Subtype.coe_mk]
    Â· split_ifs
      exacts [le_rfl, zero_le_one]
  Â· refine le_antisymm ?_ ?_
    Â· refine (ciInf_le h_bdd âŸ¨t + 1, lt_add_one tâŸ©).trans ?_
      split_ifs
      exacts [zero_le_one, le_rfl]
    Â· refine le_ciInf fun x â†¦ ?_
      rw [if_neg]
      rw [not_lt] at h âŠ¢
      exact h.trans (mem_Ioi.mp x.prop).le

lemma measurable_defaultRatCDF (Î± : Type*) [MeasurableSpace Î±] (q : â„š) :
    Measurable (fun (_ : Î±) â†¦ defaultRatCDF q) := measurable_const

lemma isCDFLike_defaultRatCDF (Î± : Type*) [MeasurableSpace Î±] :
    IsCDFLike (fun (_ : Î±) (q : â„š) â†¦ defaultRatCDF q) where
  mono _ := monotone_defaultRatCDF
  nonneg _ := defaultRatCDF_nonneg
  le_one _ := defaultRatCDF_le_one
  tendsto_atBot_zero _ := tendsto_defaultRatCDF_atBot
  tendsto_atTop_one _ := tendsto_defaultRatCDF_atTop
  iInf_rat_gt_eq _ := inf_gt_rat_defaultRatCDF
  measurable := measurable_defaultRatCDF Î±

end DefaultRatCDF

section ToCDFLike

variable {f : Î± â†’ â„š â†’ â„}

open Classical in
noncomputable
def toCDFLike (f : Î± â†’ â„š â†’ â„) : Î± â†’ â„š â†’ â„ := fun a q â†¦
  if IsRatStieltjesPoint f a then f a q else defaultRatCDF q

lemma toCDFLike_of_isRatStieltjesPoint {a : Î±} (h : IsRatStieltjesPoint f a) (q : â„š) :
    toCDFLike f a q = f a q := by
  unfold toCDFLike; simp [h]

lemma isCDFLike_toCDFLike (hf : âˆ€ q, Measurable fun a â†¦ f a q) :
    IsCDFLike (toCDFLike f) where
  mono a := by unfold toCDFLike; split_ifs with h; exacts [h.mono, monotone_defaultRatCDF]
  nonneg a := by unfold toCDFLike; split_ifs with h; exacts [h.nonneg, defaultRatCDF_nonneg]
  le_one a := by unfold toCDFLike; split_ifs with h; exacts [h.le_one, defaultRatCDF_le_one]
  tendsto_atTop_one a := by
    unfold toCDFLike; split_ifs with h; exacts [h.tendsto_atTop_one, tendsto_defaultRatCDF_atTop]
  tendsto_atBot_zero a := by
    unfold toCDFLike; split_ifs with h; exacts [h.tendsto_atBot_zero, tendsto_defaultRatCDF_atBot]
  iInf_rat_gt_eq a := by
    unfold toCDFLike; split_ifs with h; exacts [h.iInf_rat_gt_eq, inf_gt_rat_defaultRatCDF]
  measurable q :=
    Measurable.ite (measurableSet_isRatStieltjesPoint hf) (hf q) (measurable_defaultRatCDF Î± q)

lemma toCDFLike_unit_prod (a : Î±) :
    toCDFLike (fun (p : Unit Ã— Î±) â†¦ f p.2) ((), a) = toCDFLike f a := by
  unfold toCDFLike
  rw [isRatStieltjesPoint_unit_prod_iff]

end ToCDFLike

section IsCDFLike.stieltjesFunction

variable {f : Î± â†’ â„š â†’ â„} (hf : IsCDFLike f)

noncomputable irreducible_def IsCDFLike.stieltjesFunctionAux (f : Î± â†’ â„š â†’ â„) : Î± â†’ â„ â†’ â„ :=
  fun a t â†¦ â¨… r : { r' : â„š // t < r' }, f a r

lemma IsCDFLike.stieltjesFunctionAux_def' (f : Î± â†’ â„š â†’ â„) (a : Î±) :
    IsCDFLike.stieltjesFunctionAux f a = fun (t : â„) â†¦ â¨… r : { r' : â„š // t < r' }, f a r := by
  ext t; exact IsCDFLike.stieltjesFunctionAux_def f a t

lemma IsCDFLike.stieltjesFunctionAux_eq (a : Î±) (r : â„š) :
    IsCDFLike.stieltjesFunctionAux f a r = f a r := by
  rw [â† hf.iInf_rat_gt_eq a r, IsCDFLike.stieltjesFunctionAux]
  refine Equiv.iInf_congr ?_ ?_
  Â· exact
      { toFun := fun t â†¦ âŸ¨t.1, mod_cast t.2âŸ©
        invFun := fun t â†¦ âŸ¨t.1, mod_cast t.2âŸ©
        left_inv := fun t â†¦ by simp only [Subtype.coe_eta]
        right_inv := fun t â†¦ by simp only [Subtype.coe_eta] }
  Â· intro t
    simp only [Equiv.coe_fn_mk, Subtype.coe_mk]

lemma IsCDFLike.stieltjesFunctionAux_unit_prod (a : Î±) :
    IsCDFLike.stieltjesFunctionAux (fun (p : Unit Ã— Î±) â†¦ f p.2) ((), a) =
  IsCDFLike.stieltjesFunctionAux f a := by simp_rw [IsCDFLike.stieltjesFunctionAux_def']

lemma IsCDFLike.stieltjesFunctionAux_nonneg (a : Î±) (r : â„) :
    0 â‰¤ IsCDFLike.stieltjesFunctionAux f a r := by
  have : Nonempty { r' : â„š // r < â†‘r' } := by
    obtain âŸ¨r, hrxâŸ© := exists_rat_gt r
    exact âŸ¨âŸ¨r, hrxâŸ©âŸ©
  rw [IsCDFLike.stieltjesFunctionAux_def]
  exact le_ciInf fun r' â†¦ hf.nonneg a _

lemma bddBelow_range_gt (a : Î±) (x : â„) :
    BddBelow (range fun r : { r' : â„š // x < â†‘r' } â†¦ f a r) := by
  refine âŸ¨0, fun z â†¦ ?_âŸ©; rintro âŸ¨u, rflâŸ©; exact hf.nonneg a _

lemma IsCDFLike.monotone_stieltjesFunctionAux (a : Î±) :
    Monotone (IsCDFLike.stieltjesFunctionAux f a) := by
  intro x y hxy
  have : Nonempty { r' : â„š // y < â†‘r' } := by
    obtain âŸ¨r, hrxâŸ© := exists_rat_gt y
    exact âŸ¨âŸ¨r, hrxâŸ©âŸ©
  simp_rw [IsCDFLike.stieltjesFunctionAux_def]
  refine le_ciInf fun r â†¦ (ciInf_le ?_ ?_).trans_eq ?_
  Â· exact bddBelow_range_gt hf a x
  Â· exact âŸ¨r.1, hxy.trans_lt r.propâŸ©
  Â· rfl

lemma  IsCDFLike.continuousWithinAt_stieltjesFunctionAux_Ici (a : Î±) (x : â„) :
    ContinuousWithinAt (IsCDFLike.stieltjesFunctionAux f a) (Ici x) x := by
  rw [â† continuousWithinAt_Ioi_iff_Ici]
  convert Monotone.tendsto_nhdsWithin_Ioi (monotone_stieltjesFunctionAux hf a) x
  rw [sInf_image']
  have h' : â¨… r : Ioi x, stieltjesFunctionAux f a r
      = â¨… r : { r' : â„š // x < r' }, stieltjesFunctionAux f a r := by
    refine Real.iInf_Ioi_eq_iInf_rat_gt x ?_ (monotone_stieltjesFunctionAux hf a)
    refine âŸ¨0, fun z â†¦ ?_âŸ©
    rintro âŸ¨u, -, rflâŸ©
    exact stieltjesFunctionAux_nonneg hf a u
  have h'' :
    â¨… r : { r' : â„š // x < r' }, stieltjesFunctionAux f a r =
      â¨… r : { r' : â„š // x < r' }, f a r := by
    congr with r
    exact stieltjesFunctionAux_eq hf a r
  rw [h', h'', ContinuousWithinAt]
  congr!
  rw [stieltjesFunctionAux_def]

noncomputable def IsCDFLike.stieltjesFunction (a : Î±) : StieltjesFunction where
  toFun := stieltjesFunctionAux f a
  mono' := monotone_stieltjesFunctionAux hf a
  right_continuous' x := continuousWithinAt_stieltjesFunctionAux_Ici hf a x

lemma IsCDFLike.stieltjesFunction_eq (a : Î±) (r : â„š) : hf.stieltjesFunction a r = f a r :=
  stieltjesFunctionAux_eq hf a r

lemma IsCDFLike.stieltjesFunction_nonneg (a : Î±) (r : â„) : 0 â‰¤ hf.stieltjesFunction a r :=
  stieltjesFunctionAux_nonneg hf a r

lemma IsCDFLike.stieltjesFunction_le_one (a : Î±) (x : â„) : hf.stieltjesFunction a x â‰¤ 1 := by
  obtain âŸ¨r, hrxâŸ© := exists_rat_gt x
  rw [â† StieltjesFunction.iInf_rat_gt_eq]
  simp_rw [IsCDFLike.stieltjesFunction_eq]
  refine ciInf_le_of_le (bddBelow_range_gt hf a x) ?_ (hf.le_one _ _)
  exact âŸ¨r, hrxâŸ©

lemma IsCDFLike.tendsto_stieltjesFunction_atBot (a : Î±) :
    Tendsto (hf.stieltjesFunction a) atBot (ğ“ 0) := by
  have h_exists : âˆ€ x : â„, âˆƒ q : â„š, x < q âˆ§ â†‘q < x + 1 := fun x â†¦ exists_rat_btwn (lt_add_one x)
  let qs : â„ â†’ â„š := fun x â†¦ (h_exists x).choose
  have hqs_tendsto : Tendsto qs atBot atBot := by
    rw [tendsto_atBot_atBot]
    refine fun q â†¦ âŸ¨q - 1, fun y hy â†¦ ?_âŸ©
    have h_le : â†‘(qs y) â‰¤ (q : â„) - 1 + 1 :=
      (h_exists y).choose_spec.2.le.trans (add_le_add hy le_rfl)
    rw [sub_add_cancel] at h_le
    exact mod_cast h_le
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds
    ((hf.tendsto_atBot_zero a).comp hqs_tendsto) (stieltjesFunction_nonneg hf a) fun x â†¦ ?_
  rw [Function.comp_apply, â† stieltjesFunction_eq hf]
  exact (hf.stieltjesFunction a).mono (h_exists x).choose_spec.1.le

lemma IsCDFLike.tendsto_stieltjesFunction_atTop (a : Î±) :
    Tendsto (hf.stieltjesFunction a) atTop (ğ“ 1) := by
  have h_exists : âˆ€ x : â„, âˆƒ q : â„š, x - 1 < q âˆ§ â†‘q < x := fun x â†¦ exists_rat_btwn (sub_one_lt x)
  let qs : â„ â†’ â„š := fun x â†¦ (h_exists x).choose
  have hqs_tendsto : Tendsto qs atTop atTop := by
    rw [tendsto_atTop_atTop]
    refine fun q â†¦ âŸ¨q + 1, fun y hy â†¦ ?_âŸ©
    have h_le : y - 1 â‰¤ qs y := (h_exists y).choose_spec.1.le
    rw [sub_le_iff_le_add] at h_le
    exact_mod_cast le_of_add_le_add_right (hy.trans h_le)
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le ((hf.tendsto_atTop_one a).comp hqs_tendsto)
      tendsto_const_nhds ?_ (stieltjesFunction_le_one hf a)
  intro x
  rw [Function.comp_apply, â† stieltjesFunction_eq hf]
  exact (hf.stieltjesFunction a).mono (le_of_lt (h_exists x).choose_spec.2)

lemma IsCDFLike.measurable_stieltjesFunction (x : â„) :
    Measurable fun a â†¦ hf.stieltjesFunction a x := by
  have : (fun a â†¦ hf.stieltjesFunction a x) = fun a â†¦ â¨… r : { r' : â„š // x < r' }, f a â†‘r := by
    ext1 a
    rw [â† StieltjesFunction.iInf_rat_gt_eq]
    congr with q
    rw [stieltjesFunction_eq]
  rw [this]
  exact measurable_iInf (fun q â†¦ hf.measurable q)

lemma IsCDFLike.stronglyMeasurable_stieltjesFunction (x : â„) :
    StronglyMeasurable fun a â†¦ hf.stieltjesFunction a x :=
  (measurable_stieltjesFunction hf x).stronglyMeasurable

section Measure

lemma IsCDFLike.measure_stieltjesFunction_Iic (a : Î±) (x : â„) :
    (hf.stieltjesFunction a).measure (Iic x) = ENNReal.ofReal (hf.stieltjesFunction a x) := by
  rw [â† sub_zero (hf.stieltjesFunction a x)]
  exact (hf.stieltjesFunction a).measure_Iic (tendsto_stieltjesFunction_atBot hf a) _

lemma IsCDFLike.measure_stieltjesFunction_univ (a : Î±) :
    (hf.stieltjesFunction a).measure univ = 1 := by
  rw [â† ENNReal.ofReal_one, â† sub_zero (1 : â„)]
  exact StieltjesFunction.measure_univ _ (tendsto_stieltjesFunction_atBot hf a)
    (tendsto_stieltjesFunction_atTop hf a)

instance IsCDFLike.instIsProbabilityMeasure_stieltjesFunction (a : Î±) :
    IsProbabilityMeasure (hf.stieltjesFunction a).measure :=
  âŸ¨measure_stieltjesFunction_univ hf aâŸ©

lemma IsCDFLike.measurable_measure_stieltjesFunction :
    Measurable fun a â†¦ (hf.stieltjesFunction a).measure := by
  rw [Measure.measurable_measure]
  refine fun s hs â†¦ MeasurableSpace.induction_on_inter
    (C := fun s â†¦ Measurable fun b â†¦ StieltjesFunction.measure (hf.stieltjesFunction b) s)
    (borel_eq_generateFrom_Iic â„) isPiSystem_Iic ?_ ?_ ?_ ?_ hs
  Â· simp only [measure_empty, measurable_const]
  Â· rintro S âŸ¨u, rflâŸ©
    simp_rw [measure_stieltjesFunction_Iic hf _ u]
    exact (measurable_stieltjesFunction hf u).ennreal_ofReal
  Â· intro t ht ht_cd_meas
    have : (fun a â†¦ (hf.stieltjesFunction a).measure tá¶œ) =
        (fun a â†¦ (hf.stieltjesFunction a).measure univ)
          - fun a â†¦ (hf.stieltjesFunction a).measure t := by
      ext1 a
      rw [measure_compl ht (measure_ne_top (hf.stieltjesFunction a).measure _), Pi.sub_apply]
    simp_rw [this, measure_stieltjesFunction_univ hf]
    exact Measurable.sub measurable_const ht_cd_meas
  Â· intro f hf_disj hf_meas hf_cd_meas
    simp_rw [measure_iUnion hf_disj hf_meas]
    exact Measurable.ennreal_tsum hf_cd_meas

end Measure

end IsCDFLike.stieltjesFunction

section stieltjesOfMeasurableRat

variable {f : Î± â†’ â„š â†’ â„}

noncomputable
def stieltjesOfMeasurableRat (f : Î± â†’ â„š â†’ â„) (hf : âˆ€ q, Measurable fun a â†¦ f a q) :
    Î± â†’ StieltjesFunction :=
  (isCDFLike_toCDFLike hf).stieltjesFunction

lemma stieltjesOfMeasurableRat_eq (hf : âˆ€ q, Measurable fun a â†¦ f a q) (a : Î±) (r : â„š) :
    stieltjesOfMeasurableRat f hf a r = toCDFLike f a r := IsCDFLike.stieltjesFunction_eq _ a r

lemma stieltjesOfMeasurableRat_unit_prod (hf : âˆ€ q, Measurable fun a â†¦ f a q) (a : Î±) :
    stieltjesOfMeasurableRat (fun (p : Unit Ã— Î±) â†¦ f p.2)
        (fun q â†¦ (hf q).comp measurable_snd) ((), a)
      = stieltjesOfMeasurableRat f hf a := by
  simp_rw [stieltjesOfMeasurableRat,IsCDFLike.stieltjesFunction,
    â† IsCDFLike.stieltjesFunctionAux_unit_prod a]
  congr with x
  congr 1 with p : 1
  cases p with
  | mk _ b => rw [â† toCDFLike_unit_prod b]

lemma stieltjesOfMeasurableRat_nonneg (hf : âˆ€ q, Measurable fun a â†¦ f a q) (a : Î±) (r : â„) :
    0 â‰¤ stieltjesOfMeasurableRat f hf a r := IsCDFLike.stieltjesFunction_nonneg _ a r

lemma stieltjesOfMeasurableRat_le_one (hf : âˆ€ q, Measurable fun a â†¦ f a q) (a : Î±) (x : â„) :
    stieltjesOfMeasurableRat f hf a x â‰¤ 1 := IsCDFLike.stieltjesFunction_le_one _ a x

lemma tendsto_stieltjesOfMeasurableRat_atBot (hf : âˆ€ q, Measurable fun a â†¦ f a q) (a : Î±) :
    Tendsto (stieltjesOfMeasurableRat f hf a) atBot (ğ“ 0) :=
  IsCDFLike.tendsto_stieltjesFunction_atBot _ a

lemma tendsto_stieltjesOfMeasurableRat_atTop (hf : âˆ€ q, Measurable fun a â†¦ f a q) (a : Î±) :
    Tendsto (stieltjesOfMeasurableRat f hf a) atTop (ğ“ 1) :=
  IsCDFLike.tendsto_stieltjesFunction_atTop _ a

lemma measurable_stieltjesOfMeasurableRat (hf : âˆ€ q, Measurable fun a â†¦ f a q) (x : â„) :
    Measurable fun a â†¦ stieltjesOfMeasurableRat f hf a x :=
  IsCDFLike.measurable_stieltjesFunction _ x

lemma stronglyMeasurable_stieltjesOfMeasurableRat (hf : âˆ€ q, Measurable fun a â†¦ f a q) (x : â„) :
    StronglyMeasurable fun a â†¦ stieltjesOfMeasurableRat f hf a x :=
  IsCDFLike.stronglyMeasurable_stieltjesFunction _ x

section Measure

lemma measure_stieltjesOfMeasurableRat_Iic (hf : âˆ€ q, Measurable fun a â†¦ f a q) (a : Î±) (x : â„) :
    (stieltjesOfMeasurableRat f hf a).measure (Iic x)
      = ENNReal.ofReal (stieltjesOfMeasurableRat f hf a x) :=
  IsCDFLike.measure_stieltjesFunction_Iic _ _ _

lemma measure_stieltjesOfMeasurableRat_univ (hf : âˆ€ q, Measurable fun a â†¦ f a q) (a : Î±) :
    (stieltjesOfMeasurableRat f hf a).measure univ = 1 :=
  IsCDFLike.measure_stieltjesFunction_univ _ _

instance instIsProbabilityMeasure_stieltjesOfMeasurableRat
    (hf : âˆ€ q, Measurable fun a â†¦ f a q) (a : Î±) :
    IsProbabilityMeasure (stieltjesOfMeasurableRat f hf a).measure :=
  IsCDFLike.instIsProbabilityMeasure_stieltjesFunction _ _

lemma measurable_measure_stieltjesOfMeasurableRat (hf : âˆ€ q, Measurable fun a â†¦ f a q) :
    Measurable fun a â†¦ (stieltjesOfMeasurableRat f hf a).measure :=
  IsCDFLike.measurable_measure_stieltjesFunction _

end Measure

end stieltjesOfMeasurableRat
