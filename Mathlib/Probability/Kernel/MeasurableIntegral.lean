/-
Copyright (c) 2023 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Integral.DominatedConvergence
import Mathlib.Probability.Kernel.MeasurableLIntegral

/-!
# Measurability of the integral against a kernel

The Bochner integral of a strongly measurable function against a kernel is strongly measurable.

## Main statements

* `MeasureTheory.StronglyMeasurable.integral_kernel_prod_right`: the function
  `a â†¦ âˆ« b, f a b âˆ‚(Îº a)` is measurable, for an s-finite kernel `Îº : Kernel Î± Î²` and a function
  `f : Î± â†’ Î² â†’ E` such that `uncurry f` is measurable.

-/


open MeasureTheory ProbabilityTheory Function Set Filter

open scoped MeasureTheory ENNReal Topology

variable {Î± Î² Î³ : Type*} {mÎ± : MeasurableSpace Î±} {mÎ² : MeasurableSpace Î²} {mÎ³ : MeasurableSpace Î³}
  {Îº : Kernel Î± Î²} {Î· : Kernel Î² Î³} {a : Î±} {E : Type*} [NormedAddCommGroup E]

theorem ProbabilityTheory.measurableSet_integrable â¦ƒf : Î² â†’ Eâ¦„ (hf : StronglyMeasurable f) :
    MeasurableSet {a | Integrable f (Îº a)} := by
  simp_rw [Integrable, hf.aestronglyMeasurable, true_and]
  exact measurableSet_lt hf.enorm.lintegral_kernel measurable_const

variable [IsSFiniteKernel Îº] {Î· : Kernel (Î± Ã— Î²) Î³} [IsSFiniteKernel Î·]

theorem ProbabilityTheory.measurableSet_kernel_integrable â¦ƒf : Î± â†’ Î² â†’ Eâ¦„
    (hf : StronglyMeasurable (uncurry f)) :
    MeasurableSet {x | Integrable (f x) (Îº x)} := by
  simp_rw [Integrable, hf.of_uncurry_left.aestronglyMeasurable, true_and]
  exact measurableSet_lt (Measurable.lintegral_kernel_prod_right hf.enorm) measurable_const

open ProbabilityTheory.Kernel

namespace MeasureTheory

variable [NormedSpace â„ E]

omit [IsSFiniteKernel Îº] in
theorem StronglyMeasurable.integral_kernel â¦ƒf : Î² â†’ Eâ¦„
    (hf : StronglyMeasurable f) : StronglyMeasurable fun x â†¦ âˆ« y, f y âˆ‚Îº x := by
  classical
  by_cases hE : CompleteSpace E; swap
  Â· simp [integral, hE, stronglyMeasurable_const]
  borelize E
  have : TopologicalSpace.SeparableSpace (range f âˆª {0} : Set E) :=
    hf.separableSpace_range_union_singleton
  let s : â„• â†’ SimpleFunc Î² E := SimpleFunc.approxOn _ hf.measurable (range f âˆª {0}) 0 (by simp)
  let f' n : Î± â†’ E := {x | Integrable f (Îº x)}.indicator fun x â†¦ (s n).integral (Îº x)
  refine stronglyMeasurable_of_tendsto (f := f') atTop (fun n â†¦ ?_) ?_
  Â· refine StronglyMeasurable.indicator ?_ (measurableSet_integrable hf)
    simp_rw [SimpleFunc.integral_eq]
    refine Finset.stronglyMeasurable_fun_sum _ fun _ _ â†¦ ?_
    refine (Measurable.ennreal_toReal ?_).stronglyMeasurable.smul_const _
    exact Îº.measurable_coe ((s n).measurableSet_fiber _)
  Â· rw [tendsto_pi_nhds]; intro x
    by_cases hfx : Integrable f (Îº x)
    Â· simp only [mem_setOf_eq, hfx, indicator_of_mem, f']
      apply tendsto_integral_approxOn_of_measurable_of_range_subset _ hfx
      exact subset_rfl
    Â· simp [f', hfx, integral_undef]

theorem StronglyMeasurable.integral_kernel_prod_right â¦ƒf : Î± â†’ Î² â†’ Eâ¦„
    (hf : StronglyMeasurable (uncurry f)) : StronglyMeasurable fun x => âˆ« y, f x y âˆ‚Îº x := by
  classical
  by_cases hE : CompleteSpace E; swap
  Â· simp [integral, hE, stronglyMeasurable_const]
  borelize E
  haveI : TopologicalSpace.SeparableSpace (range (uncurry f) âˆª {0} : Set E) :=
    hf.separableSpace_range_union_singleton
  let s : â„• â†’ SimpleFunc (Î± Ã— Î²) E :=
    SimpleFunc.approxOn _ hf.measurable (range (uncurry f) âˆª {0}) 0 (by simp)
  let s' : â„• â†’ Î± â†’ SimpleFunc Î² E := fun n x => (s n).comp (Prod.mk x) measurable_prodMk_left
  let f' : â„• â†’ Î± â†’ E := fun n =>
    {x | Integrable (f x) (Îº x)}.indicator fun x => (s' n x).integral (Îº x)
  have hf' : âˆ€ n, StronglyMeasurable (f' n) := by
    intro n; refine StronglyMeasurable.indicator ?_ (measurableSet_kernel_integrable hf)
    have : âˆ€ x, ((s' n x).range.filter fun x => x â‰  0) âŠ† (s n).range := by
      intro x; refine Finset.Subset.trans (Finset.filter_subset _ _) ?_; intro y
      simp_rw [SimpleFunc.mem_range]; rintro âŸ¨z, rflâŸ©; exact âŸ¨(x, z), rflâŸ©
    simp only [SimpleFunc.integral_eq_sum_of_subset (this _)]
    refine Finset.stronglyMeasurable_fun_sum _ fun x _ => ?_
    refine (Measurable.ennreal_toReal ?_).stronglyMeasurable.smul_const _
    simp only [s', SimpleFunc.coe_comp, preimage_comp]
    apply Kernel.measurable_kernel_prodMk_left
    exact (s n).measurableSet_fiber x
  have h2f' : Tendsto f' atTop (ğ“ fun x : Î± => âˆ« y : Î², f x y âˆ‚Îº x) := by
    rw [tendsto_pi_nhds]; intro x
    by_cases hfx : Integrable (f x) (Îº x)
    Â· have (n : _) : Integrable (s' n x) (Îº x) := by
        apply (hfx.norm.add hfx.norm).mono' (s' n x).aestronglyMeasurable
        filter_upwards with y
        simp_rw [s', SimpleFunc.coe_comp]; exact SimpleFunc.norm_approxOn_zero_le _ _ (x, y) n
      simp only [f', hfx, SimpleFunc.integral_eq_integral _ (this _), indicator_of_mem,
        mem_setOf_eq]
      refine
        tendsto_integral_of_dominated_convergence (fun y => â€–f x yâ€– + â€–f x yâ€–)
          (fun n => (s' n x).aestronglyMeasurable) (hfx.norm.add hfx.norm) ?_ ?_
      Â· -- Porting note: was
        -- exact fun n => Eventually.of_forall fun y =>
        --   SimpleFunc.norm_approxOn_zero_le _ _ (x, y) n
        exact fun n => Eventually.of_forall fun y =>
          SimpleFunc.norm_approxOn_zero_le hf.measurable (by simp) (x, y) n
      Â· refine Eventually.of_forall fun y => SimpleFunc.tendsto_approxOn hf.measurable (by simp) ?_
        apply subset_closure
        simp [-uncurry_apply_pair]
    Â· simp [f', hfx, integral_undef]
  exact stronglyMeasurable_of_tendsto _ hf' h2f'

theorem StronglyMeasurable.integral_kernel_prod_right' â¦ƒf : Î± Ã— Î² â†’ Eâ¦„ (hf : StronglyMeasurable f) :
    StronglyMeasurable fun x => âˆ« y, f (x, y) âˆ‚Îº x := by
  rw [â† uncurry_curry f] at hf
  exact hf.integral_kernel_prod_right

theorem StronglyMeasurable.integral_kernel_prod_right'' {f : Î² Ã— Î³ â†’ E}
    (hf : StronglyMeasurable f) : StronglyMeasurable fun x => âˆ« y, f (x, y) âˆ‚Î· (a, x) := by
  change
    StronglyMeasurable
      ((fun x => âˆ« y, (fun u : (Î± Ã— Î²) Ã— Î³ => f (u.1.2, u.2)) (x, y) âˆ‚Î· x) âˆ˜ fun x => (a, x))
  apply StronglyMeasurable.comp_measurable _ (measurable_prodMk_left (m := mÎ±))
  Â· have := MeasureTheory.StronglyMeasurable.integral_kernel_prod_right' (Îº := Î·)
      (hf.comp_measurable (measurable_fst.snd.prodMk measurable_snd))
    simpa using this

theorem StronglyMeasurable.integral_kernel_prod_left â¦ƒf : Î² â†’ Î± â†’ Eâ¦„
    (hf : StronglyMeasurable (uncurry f)) : StronglyMeasurable fun y => âˆ« x, f x y âˆ‚Îº y :=
  (hf.comp_measurable measurable_swap).integral_kernel_prod_right'

theorem StronglyMeasurable.integral_kernel_prod_left' â¦ƒf : Î² Ã— Î± â†’ Eâ¦„ (hf : StronglyMeasurable f) :
    StronglyMeasurable fun y => âˆ« x, f (x, y) âˆ‚Îº y :=
  (hf.comp_measurable measurable_swap).integral_kernel_prod_right'

theorem StronglyMeasurable.integral_kernel_prod_left'' {f : Î³ Ã— Î² â†’ E} (hf : StronglyMeasurable f) :
    StronglyMeasurable fun y => âˆ« x, f (x, y) âˆ‚Î· (a, y) := by
  change
    StronglyMeasurable
      ((fun y => âˆ« x, (fun u : Î³ Ã— Î± Ã— Î² => f (u.1, u.2.2)) (x, y) âˆ‚Î· y) âˆ˜ fun x => (a, x))
  apply StronglyMeasurable.comp_measurable _ (measurable_prodMk_left (m := mÎ±))
  Â· have := MeasureTheory.StronglyMeasurable.integral_kernel_prod_left' (Îº := Î·)
      (hf.comp_measurable (measurable_fst.prodMk measurable_snd.snd))
    simpa using this

end MeasureTheory
