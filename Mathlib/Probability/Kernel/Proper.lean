/-
Copyright (c) 2024 YaÃ«l Dillies, Kalle KytÃ¶lÃ¤, Kin Yau James Wong. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Kalle KytÃ¶lÃ¤, Kin Yau James Wong
-/
import Mathlib.Probability.Kernel.Composition.CompNotation

/-!
# Proper kernels

This file defines properness of measure kernels.

For two Ïƒ-algebras `ğ“‘ â‰¤ ğ“§`, a `ğ“‘, ğ“§`-kernel `Ï€ : X â†’ Measure X` is proper if
`âˆ« x, g x * f x âˆ‚(Ï€ xâ‚€) = g xâ‚€ * âˆ« x, f x âˆ‚(Ï€ xâ‚€)` for all `xâ‚€ : X`, `ğ“§`-measurable function `f`
and `ğ“‘`-measurable function `g`.

By the standard machine, this is equivalent to having that, for all `B âˆˆ ğ“‘`, `Ï€` restricted to `B`
is the same as `Ï€` times the indicator of `B`.

This should be thought of as the condition under which one can meaningfully restrict a kernel to an
event.

## TODO

Prove the `integral` versions of the `lintegral` lemmas below
-/

open MeasureTheory ENNReal NNReal Set
open scoped ProbabilityTheory

namespace ProbabilityTheory.Kernel
variable {X : Type*} {ğ“‘ ğ“§ : MeasurableSpace X} {Ï€ : Kernel[ğ“‘, ğ“§] X X} {A B : Set X}
  {f g : X â†’ â„â‰¥0âˆ} {xâ‚€ : X}

/-- For two Ïƒ-algebras `ğ“‘ â‰¤ ğ“§` on a space `X`, a `ğ“‘, ğ“§`-kernel `Ï€ : X â†’ Measure X` is proper if
`âˆ« x, g x * f x âˆ‚(Ï€ xâ‚€) = g xâ‚€ * âˆ« x, f x âˆ‚(Ï€ xâ‚€)` for all `xâ‚€ : X`, `ğ“§`-measurable function `f`
and `ğ“‘`-measurable function `g`.

By the standard machine, this is equivalent to having that, for all `B âˆˆ ğ“‘`, `Ï€` restricted to `B`
is the same as `Ï€` times the indicator of `B`.

To avoid assuming `ğ“‘ â‰¤ ğ“§` in the definition, we replace `ğ“‘` by `ğ“‘ âŠ“ ğ“§` in the restriction. -/
structure IsProper (Ï€ : Kernel[ğ“‘, ğ“§] X X) : Prop where
  restrict_eq_indicator_smul' :
    âˆ€ â¦ƒB : Set Xâ¦„ (hB : MeasurableSet[ğ“‘ âŠ“ ğ“§] B) (x : X),
      Ï€.restrict (inf_le_right (b := ğ“§) _ hB) x = B.indicator (fun _ â†¦ (1 : â„â‰¥0âˆ)) x â€¢ Ï€ x

lemma isProper_iff_restrict_eq_indicator_smul (hğ“‘ğ“§ : ğ“‘ â‰¤ ğ“§) :
    IsProper Ï€ â†” âˆ€ â¦ƒB : Set Xâ¦„ (hB : MeasurableSet[ğ“‘] B) (x : X),
      Ï€.restrict (hğ“‘ğ“§ _ hB) x = B.indicator (fun _ â†¦ (1 : â„â‰¥0âˆ)) x â€¢ Ï€ x := by
  refine âŸ¨fun âŸ¨hâŸ© â†¦ ?_, fun h â†¦ âŸ¨?_âŸ©âŸ© <;> simpa only [inf_eq_left.2 hğ“‘ğ“§] using h

lemma isProper_iff_inter_eq_indicator_mul (hğ“‘ğ“§ : ğ“‘ â‰¤ ğ“§) :
    IsProper Ï€ â†”
      âˆ€ â¦ƒA : Set Xâ¦„ (_hA : MeasurableSet[ğ“§] A) â¦ƒB : Set Xâ¦„ (_hB : MeasurableSet[ğ“‘] B) (x : X),
        Ï€ x (A âˆ© B) = B.indicator 1 x * Ï€ x A := by
  calc
    _ â†” âˆ€ â¦ƒA : Set Xâ¦„ (_hA : MeasurableSet[ğ“§] A) â¦ƒB : Set Xâ¦„ (hB : MeasurableSet[ğ“‘] B) (x : X),
          Ï€.restrict (hğ“‘ğ“§ _ hB) x A = B.indicator 1 x * Ï€ x A := by
      simp [isProper_iff_restrict_eq_indicator_smul hğ“‘ğ“§, Measure.ext_iff]; aesop
    _ â†” _ := by congr! 5 with A hA B hB x; rw [restrict_apply, Measure.restrict_apply hA]

alias âŸ¨IsProper.restrict_eq_indicator_smul, IsProper.of_restrict_eq_indicator_smulâŸ© :=
  isProper_iff_restrict_eq_indicator_smul

alias âŸ¨IsProper.inter_eq_indicator_mul, IsProper.of_inter_eq_indicator_mulâŸ© :=
  isProper_iff_inter_eq_indicator_mul

lemma IsProper.setLIntegral_eq_comp (hÏ€ : IsProper Ï€) (hğ“‘ğ“§ : ğ“‘ â‰¤ ğ“§) {Î¼ : Measure[ğ“§] X}
    (hA : MeasurableSet[ğ“§] A) (hB : MeasurableSet[ğ“‘] B) :
    âˆ«â» a in B, Ï€ a A âˆ‚Î¼ = (Ï€ âˆ˜â‚˜ Î¼) (A âˆ© B) := by
  rw [Measure.bind_apply (by measurability) (Ï€.measurable.mono hğ“‘ğ“§ le_rfl).aemeasurable]
  simp only [hÏ€.inter_eq_indicator_mul hğ“‘ğ“§ hA hB, â† indicator_mul_const, Pi.one_apply, one_mul]
  rw [â† lintegral_indicator (hğ“‘ğ“§ _ hB)]
  rfl

@[deprecated (since := "2025-01-24")]
alias IsProper.setLIntegral_eq_bind := IsProper.setLIntegral_eq_comp

/-- Auxiliary lemma for `IsProper.lintegral_mul` and
`IsProper.setLIntegral_eq_indicator_mul_lintegral`. -/
private lemma IsProper.lintegral_indicator_mul_indicator (hÏ€ : IsProper Ï€) (hğ“‘ğ“§ : ğ“‘ â‰¤ ğ“§)
    (hA : MeasurableSet[ğ“§] A) (hB : MeasurableSet[ğ“‘] B) :
    âˆ«â» x, B.indicator 1 x * A.indicator 1 x âˆ‚(Ï€ xâ‚€) =
      B.indicator 1 xâ‚€ * âˆ«â» x, A.indicator 1 x âˆ‚(Ï€ xâ‚€) := by
  simp_rw [â† inter_indicator_mul]
  rw [lintegral_indicator ((hğ“‘ğ“§ _ hB).inter hA), lintegral_indicator hA]
  simp only [MeasureTheory.lintegral_const, MeasurableSet.univ, Measure.restrict_apply, univ_inter,
    Pi.one_apply, one_mul]
  rw [â† hÏ€.inter_eq_indicator_mul hğ“‘ğ“§ hA hB, inter_comm]

set_option linter.style.multiGoal false in -- false positive
/-- Auxiliary lemma for `IsProper.lintegral_mul` and
`IsProper.setLIntegral_eq_indicator_mul_lintegral`. -/
private lemma IsProper.lintegral_indicator_mul (hÏ€ : IsProper Ï€) (hğ“‘ğ“§ : ğ“‘ â‰¤ ğ“§)
    (hf : Measurable[ğ“§] f) (hB : MeasurableSet[ğ“‘] B) :
    âˆ«â» x, B.indicator 1 x * f x âˆ‚(Ï€ xâ‚€) = B.indicator 1 xâ‚€ * âˆ«â» x, f x âˆ‚(Ï€ xâ‚€) := by
  refine hf.ennreal_induction ?_ ?_ ?_
  Â· rintro c A hA
    simp_rw [â† smul_indicator_one_apply, mul_smul_comm, smul_eq_mul]
    rw [lintegral_const_mul, lintegral_const_mul, hÏ€.lintegral_indicator_mul_indicator hğ“‘ğ“§ hA hB,
      mul_left_comm] <;> measurability
  Â· rintro fâ‚ fâ‚‚ - _ _ hfâ‚ hfâ‚‚
    simp only [Pi.add_apply, mul_add]
    rw [lintegral_add_right, lintegral_add_right, hfâ‚, hfâ‚‚, mul_add] <;> measurability
  Â· rintro f' hf'_meas hf'_mono hf'
    simp_rw [ENNReal.mul_iSup]
    rw [lintegral_iSup (by measurability), lintegral_iSup hf'_meas hf'_mono, ENNReal.mul_iSup]
    simp_rw [hf']
    Â· exact hf'_mono.const_mul (zero_le _)

lemma IsProper.setLIntegral_eq_indicator_mul_lintegral (hÏ€ : IsProper Ï€) (hğ“‘ğ“§ : ğ“‘ â‰¤ ğ“§)
    (hf : Measurable[ğ“§] f) (hB : MeasurableSet[ğ“‘] B) (xâ‚€ : X) :
    âˆ«â» x in B, f x âˆ‚(Ï€ xâ‚€) = B.indicator 1 xâ‚€ * âˆ«â» x, f x âˆ‚(Ï€ xâ‚€) := by
  simp [â† hÏ€.lintegral_indicator_mul hğ“‘ğ“§ hf hB, â† indicator_mul_left,
    lintegral_indicator (hğ“‘ğ“§ _ hB)]

lemma IsProper.setLIntegral_inter_eq_indicator_mul_setLIntegral (hÏ€ : IsProper Ï€) (hğ“‘ğ“§ : ğ“‘ â‰¤ ğ“§)
    (hf : Measurable[ğ“§] f) (hA : MeasurableSet[ğ“§] A) (hB : MeasurableSet[ğ“‘] B) (xâ‚€ : X) :
    âˆ«â» x in A âˆ© B, f x âˆ‚(Ï€ xâ‚€) = B.indicator 1 xâ‚€ * âˆ«â» x in A, f x âˆ‚(Ï€ xâ‚€) := by
  rw [â† lintegral_indicator hA, â† hÏ€.setLIntegral_eq_indicator_mul_lintegral hğ“‘ğ“§ _ hB,
    setLIntegral_indicator] <;> measurability

lemma IsProper.lintegral_mul (hÏ€ : IsProper Ï€) (hğ“‘ğ“§ : ğ“‘ â‰¤ ğ“§) (hf : Measurable[ğ“§] f)
    (hg : Measurable[ğ“‘] g) (xâ‚€ : X) :
    âˆ«â» x, g x * f x âˆ‚(Ï€ xâ‚€) = g xâ‚€ * âˆ«â» x, f x âˆ‚(Ï€ xâ‚€) := by
  refine hg.ennreal_induction ?_ ?_ ?_
  Â· rintro c A hA
    simp_rw [â† smul_indicator_one_apply, smul_mul_assoc, smul_eq_mul]
    rw [lintegral_const_mul, hÏ€.lintegral_indicator_mul hğ“‘ğ“§ hf hA]
    Â· measurability
  Â· rintro gâ‚ gâ‚‚ - _ hgâ‚‚_meas hgâ‚ hgâ‚‚
    simp only [Pi.add_apply, mul_add, add_mul]
    rw [lintegral_add_right, hgâ‚, hgâ‚‚]
    Â· exact (hgâ‚‚_meas.mono hğ“‘ğ“§ le_rfl).mul hf
  Â· rintro g' hg'_meas hg'_mono hg'
    simp_rw [ENNReal.iSup_mul]
    rw [lintegral_iSup (fun n â†¦ ((hg'_meas _).mono hğ“‘ğ“§ le_rfl).mul hf)
      (hg'_mono.mul_const (zero_le _))]
    simp_rw [hg']

end ProbabilityTheory.Kernel
