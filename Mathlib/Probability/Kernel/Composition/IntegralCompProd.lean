/-
Copyright (c) 2023 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Etienne Marion
-/
import Mathlib.Probability.Kernel.Composition.MeasureComp
import Mathlib.Probability.Kernel.MeasurableIntegral

/-!
# Bochner integral of a function against the composition and the composition-products of two kernels

We prove properties of the composition and the composition-product of two kernels.

If `Îº` is a kernel from `Î±` to `Î²` and `Î·` is a kernel from `Î²` to `Î³`, we can form their
composition `Î· âˆ˜â‚– Îº : Kernel Î± Î³`. We proved in `ProbabilityTheory.Kernel.lintegral_comp` that it
verifies `âˆ«â» c, f c âˆ‚((Î· âˆ˜â‚– Îº) a) = âˆ«â» b, âˆ«â» c, f c âˆ‚(Î· b) âˆ‚(Îº a)`. In this file, we
prove the same equality for the Bochner integral.

If `Îº` is an s-finite kernel from `Î±` to `Î²` and `Î·` is an s-finite kernel from `Î± Ã— Î²` to `Î³`,
we can form their composition-product `Îº âŠ—â‚– Î· : Kernel Î± (Î² Ã— Î³)`.
We proved in `ProbabilityTheory.Kernel.lintegral_compProd` that it
verifies `âˆ«â» bc, f bc âˆ‚((Îº âŠ—â‚– Î·) a) = âˆ«â» b, âˆ«â» c, f (b, c) âˆ‚(Î· (a, b)) âˆ‚(Îº a)`. In this file, we
prove the same equality for the Bochner integral.

## Main statements

* `ProbabilityTheory.integral_compProd`: the integral against the composition-product is
  `âˆ« z, f z âˆ‚((Îº âŠ—â‚– Î·) a) = âˆ« x, âˆ« y, f (x, y) âˆ‚(Î· (a, x)) âˆ‚(Îº a)`.

* `ProbabilityTheory.integral_comp`: the integral against the composition is
  `âˆ«â» z, f z âˆ‚((Î· âˆ˜â‚– Îº) a) = âˆ«â» x, âˆ«â» y, f y âˆ‚(Î· x) âˆ‚(Îº a)`.

## Implementation details

This file is to a large extent a copy of part of `Mathlib/MeasureTheory/Integral/Prod.lean`.
The product of two measures is a particular case of composition-product of kernels and
it turns out that once the measurability of the Lebesgue integral of a kernel is proved,
almost all proofs about integrals against products of measures extend with minimal modifications
to the composition-product of two kernels.

The composition of kernels can also be expressed easily with the composition-product and therefore
the proofs about the composition are only simplified versions of the ones for the
composition-product. However it is necessary to do all the proofs once again because the
composition-product requires s-finiteness while the composition does not.
-/


noncomputable section

open Set Function Real ENNReal MeasureTheory Filter ProbabilityTheory ProbabilityTheory.Kernel
open scoped Topology ENNReal MeasureTheory

variable {Î± Î² Î³ E : Type*} {mÎ± : MeasurableSpace Î±} {mÎ² : MeasurableSpace Î²}
  {mÎ³ : MeasurableSpace Î³} [NormedAddCommGroup E] {a : Î±}

namespace ProbabilityTheory

section compProd

variable {Îº : Kernel Î± Î²} [IsSFiniteKernel Îº] {Î· : Kernel (Î± Ã— Î²) Î³} [IsSFiniteKernel Î·]

theorem hasFiniteIntegral_prodMk_left (a : Î±) {s : Set (Î² Ã— Î³)} (h2s : (Îº âŠ—â‚– Î·) a s â‰  âˆ) :
    HasFiniteIntegral (fun b => (Î· (a, b)).real (Prod.mk b â»Â¹' s)) (Îº a) := by
  let t := toMeasurable ((Îº âŠ—â‚– Î·) a) s
  simp_rw [hasFiniteIntegral_iff_enorm, measureReal_def, enorm_eq_ofReal toReal_nonneg]
  calc
    âˆ«â» b, ENNReal.ofReal (Î· (a, b) (Prod.mk b â»Â¹' s)).toReal âˆ‚Îº a
    _ â‰¤ âˆ«â» b, Î· (a, b) (Prod.mk b â»Â¹' t) âˆ‚Îº a := by
      refine lintegral_mono_ae ?_
      filter_upwards [ae_kernel_lt_top a h2s] with b hb
      rw [ofReal_toReal hb.ne]
      exact measure_mono (preimage_mono (subset_toMeasurable _ _))
    _ â‰¤ (Îº âŠ—â‚– Î·) a t := le_compProd_apply _ _ _ _
    _ = (Îº âŠ—â‚– Î·) a s := measure_toMeasurable s
    _ < âŠ¤ := h2s.lt_top

@[deprecated (since := "2025-03-05")]
alias hasFiniteIntegral_prod_mk_left := hasFiniteIntegral_prodMk_left

theorem integrable_kernel_prodMk_left (a : Î±) {s : Set (Î² Ã— Î³)} (hs : MeasurableSet s)
    (h2s : (Îº âŠ—â‚– Î·) a s â‰  âˆ) : Integrable (fun b => (Î· (a, b)).real (Prod.mk b â»Â¹' s)) (Îº a) := by
  constructor
  Â· exact (measurable_kernel_prodMk_left' hs a).ennreal_toReal.aestronglyMeasurable
  Â· exact hasFiniteIntegral_prodMk_left a h2s

@[deprecated (since := "2025-03-05")]
alias integrable_kernel_prod_mk_left := integrable_kernel_prodMk_left

theorem _root_.MeasureTheory.AEStronglyMeasurable.integral_kernel_compProd [NormedSpace â„ E]
    â¦ƒf : Î² Ã— Î³ â†’ Eâ¦„ (hf : AEStronglyMeasurable f ((Îº âŠ—â‚– Î·) a)) :
    AEStronglyMeasurable (fun x => âˆ« y, f (x, y) âˆ‚Î· (a, x)) (Îº a) :=
  âŸ¨fun x => âˆ« y, hf.mk f (x, y) âˆ‚Î· (a, x), hf.stronglyMeasurable_mk.integral_kernel_prod_right'', by
    filter_upwards [ae_ae_of_ae_compProd hf.ae_eq_mk] with _ hx using integral_congr_ae hxâŸ©

theorem _root_.MeasureTheory.AEStronglyMeasurable.compProd_mk_left {Î´ : Type*} [TopologicalSpace Î´]
    {f : Î² Ã— Î³ â†’ Î´} (hf : AEStronglyMeasurable f ((Îº âŠ—â‚– Î·) a)) :
    âˆ€áµ x âˆ‚Îº a, AEStronglyMeasurable (fun y => f (x, y)) (Î· (a, x)) := by
  filter_upwards [ae_ae_of_ae_compProd hf.ae_eq_mk] with x hx using
    âŸ¨fun y => hf.mk f (x, y), hf.stronglyMeasurable_mk.comp_measurable measurable_prodMk_left, hxâŸ©

/-! ### Integrability -/


theorem hasFiniteIntegral_compProd_iff â¦ƒf : Î² Ã— Î³ â†’ Eâ¦„ (h1f : StronglyMeasurable f) :
    HasFiniteIntegral f ((Îº âŠ—â‚– Î·) a) â†”
      (âˆ€áµ x âˆ‚Îº a, HasFiniteIntegral (fun y => f (x, y)) (Î· (a, x))) âˆ§
        HasFiniteIntegral (fun x => âˆ« y, â€–f (x, y)â€– âˆ‚Î· (a, x)) (Îº a) := by
  simp only [hasFiniteIntegral_iff_enorm]
  rw [lintegral_compProd _ _ _ h1f.enorm]
  have : âˆ€ x, âˆ€áµ y âˆ‚Î· (a, x), 0 â‰¤ â€–f (x, y)â€– := fun x => Eventually.of_forall fun y => norm_nonneg _
  simp_rw [integral_eq_lintegral_of_nonneg_ae (this _)
      (h1f.norm.comp_measurable measurable_prodMk_left).aestronglyMeasurable,
    enorm_eq_ofReal toReal_nonneg, ofReal_norm_eq_enorm]
  have : âˆ€ {p q r : Prop} (_ : r â†’ p), (r â†” p âˆ§ q) â†” p â†’ (r â†” q) := fun {p q r} h1 => by
    rw [â† and_congr_right_iff, and_iff_right_of_imp h1]
  rw [this]
  Â· intro h2f; rw [lintegral_congr_ae]
    filter_upwards [h2f] with x hx
    rw [ofReal_toReal]; finiteness
  Â· intro h2f; refine ae_lt_top ?_ h2f.ne; exact h1f.enorm.lintegral_kernel_prod_right''

theorem hasFiniteIntegral_compProd_iff' â¦ƒf : Î² Ã— Î³ â†’ Eâ¦„
    (h1f : AEStronglyMeasurable f ((Îº âŠ—â‚– Î·) a)) :
    HasFiniteIntegral f ((Îº âŠ—â‚– Î·) a) â†”
      (âˆ€áµ x âˆ‚Îº a, HasFiniteIntegral (fun y => f (x, y)) (Î· (a, x))) âˆ§
        HasFiniteIntegral (fun x => âˆ« y, â€–f (x, y)â€– âˆ‚Î· (a, x)) (Îº a) := by
  rw [hasFiniteIntegral_congr h1f.ae_eq_mk,
    hasFiniteIntegral_compProd_iff h1f.stronglyMeasurable_mk]
  apply and_congr
  Â· apply eventually_congr
    filter_upwards [ae_ae_of_ae_compProd h1f.ae_eq_mk.symm] with x hx using
      hasFiniteIntegral_congr hx
  Â· apply hasFiniteIntegral_congr
    filter_upwards [ae_ae_of_ae_compProd h1f.ae_eq_mk.symm] with _ hx using
      integral_congr_ae (EventuallyEq.fun_comp hx _)

theorem integrable_compProd_iff â¦ƒf : Î² Ã— Î³ â†’ Eâ¦„ (hf : AEStronglyMeasurable f ((Îº âŠ—â‚– Î·) a)) :
    Integrable f ((Îº âŠ—â‚– Î·) a) â†”
      (âˆ€áµ x âˆ‚Îº a, Integrable (fun y => f (x, y)) (Î· (a, x))) âˆ§
        Integrable (fun x => âˆ« y, â€–f (x, y)â€– âˆ‚Î· (a, x)) (Îº a) := by
  simp only [Integrable, hasFiniteIntegral_compProd_iff' hf, hf.norm.integral_kernel_compProd,
    hf, hf.compProd_mk_left, eventually_and, true_and]

theorem _root_.MeasureTheory.Integrable.ae_of_compProd â¦ƒf : Î² Ã— Î³ â†’ Eâ¦„
    (hf : Integrable f ((Îº âŠ—â‚– Î·) a)) : âˆ€áµ x âˆ‚Îº a, Integrable (fun y => f (x, y)) (Î· (a, x)) :=
  ((integrable_compProd_iff hf.aestronglyMeasurable).mp hf).1

theorem _root_.MeasureTheory.Integrable.integral_norm_compProd â¦ƒf : Î² Ã— Î³ â†’ Eâ¦„
    (hf : Integrable f ((Îº âŠ—â‚– Î·) a)) : Integrable (fun x => âˆ« y, â€–f (x, y)â€– âˆ‚Î· (a, x)) (Îº a) :=
  ((integrable_compProd_iff hf.aestronglyMeasurable).mp hf).2

theorem _root_.MeasureTheory.Integrable.integral_compProd [NormedSpace â„ E]
    â¦ƒf : Î² Ã— Î³ â†’ Eâ¦„ (hf : Integrable f ((Îº âŠ—â‚– Î·) a)) :
    Integrable (fun x => âˆ« y, f (x, y) âˆ‚Î· (a, x)) (Îº a) :=
  Integrable.mono hf.integral_norm_compProd hf.aestronglyMeasurable.integral_kernel_compProd <|
    Eventually.of_forall fun x =>
      (norm_integral_le_integral_norm _).trans_eq <|
        (norm_of_nonneg <|
            integral_nonneg_of_ae <|
              Eventually.of_forall fun y => (norm_nonneg (f (x, y)) :)).symm

/-! ### Bochner integral -/


variable [NormedSpace â„ E] {E' : Type*} [NormedAddCommGroup E'] [NormedSpace â„ E']

theorem Kernel.integral_fn_integral_add â¦ƒf g : Î² Ã— Î³ â†’ Eâ¦„ (F : E â†’ E')
    (hf : Integrable f ((Îº âŠ—â‚– Î·) a)) (hg : Integrable g ((Îº âŠ—â‚– Î·) a)) :
    âˆ« x, F (âˆ« y, f (x, y) + g (x, y) âˆ‚Î· (a, x)) âˆ‚Îº a =
      âˆ« x, F (âˆ« y, f (x, y) âˆ‚Î· (a, x) + âˆ« y, g (x, y) âˆ‚Î· (a, x)) âˆ‚Îº a := by
  refine integral_congr_ae ?_
  filter_upwards [hf.ae_of_compProd, hg.ae_of_compProd] with _ h2f h2g
  simp [integral_add h2f h2g]

theorem Kernel.integral_fn_integral_sub â¦ƒf g : Î² Ã— Î³ â†’ Eâ¦„ (F : E â†’ E')
    (hf : Integrable f ((Îº âŠ—â‚– Î·) a)) (hg : Integrable g ((Îº âŠ—â‚– Î·) a)) :
    âˆ« x, F (âˆ« y, f (x, y) - g (x, y) âˆ‚Î· (a, x)) âˆ‚Îº a =
      âˆ« x, F (âˆ« y, f (x, y) âˆ‚Î· (a, x) - âˆ« y, g (x, y) âˆ‚Î· (a, x)) âˆ‚Îº a := by
  refine integral_congr_ae ?_
  filter_upwards [hf.ae_of_compProd, hg.ae_of_compProd] with _ h2f h2g
  simp [integral_sub h2f h2g]

theorem Kernel.lintegral_fn_integral_sub â¦ƒf g : Î² Ã— Î³ â†’ Eâ¦„ (F : E â†’ â„â‰¥0âˆ)
    (hf : Integrable f ((Îº âŠ—â‚– Î·) a)) (hg : Integrable g ((Îº âŠ—â‚– Î·) a)) :
    âˆ«â» x, F (âˆ« y, f (x, y) - g (x, y) âˆ‚Î· (a, x)) âˆ‚Îº a =
      âˆ«â» x, F (âˆ« y, f (x, y) âˆ‚Î· (a, x) - âˆ« y, g (x, y) âˆ‚Î· (a, x)) âˆ‚Îº a := by
  refine lintegral_congr_ae ?_
  filter_upwards [hf.ae_of_compProd, hg.ae_of_compProd] with _ h2f h2g
  simp [integral_sub h2f h2g]

theorem Kernel.integral_integral_add â¦ƒf g : Î² Ã— Î³ â†’ Eâ¦„ (hf : Integrable f ((Îº âŠ—â‚– Î·) a))
    (hg : Integrable g ((Îº âŠ—â‚– Î·) a)) :
    âˆ« x, âˆ« y, f (x, y) + g (x, y) âˆ‚Î· (a, x) âˆ‚Îº a =
      âˆ« x, âˆ« y, f (x, y) âˆ‚Î· (a, x) âˆ‚Îº a + âˆ« x, âˆ« y, g (x, y) âˆ‚Î· (a, x) âˆ‚Îº a :=
  (Kernel.integral_fn_integral_add id hf hg).trans <|
    integral_add hf.integral_compProd hg.integral_compProd

theorem Kernel.integral_integral_add' â¦ƒf g : Î² Ã— Î³ â†’ Eâ¦„ (hf : Integrable f ((Îº âŠ—â‚– Î·) a))
    (hg : Integrable g ((Îº âŠ—â‚– Î·) a)) :
    âˆ« x, âˆ« y, (f + g) (x, y) âˆ‚Î· (a, x) âˆ‚Îº a =
      âˆ« x, âˆ« y, f (x, y) âˆ‚Î· (a, x) âˆ‚Îº a + âˆ« x, âˆ« y, g (x, y) âˆ‚Î· (a, x) âˆ‚Îº a :=
  Kernel.integral_integral_add hf hg

theorem Kernel.integral_integral_sub â¦ƒf g : Î² Ã— Î³ â†’ Eâ¦„ (hf : Integrable f ((Îº âŠ—â‚– Î·) a))
    (hg : Integrable g ((Îº âŠ—â‚– Î·) a)) :
    âˆ« x, âˆ« y, f (x, y) - g (x, y) âˆ‚Î· (a, x) âˆ‚Îº a =
      âˆ« x, âˆ« y, f (x, y) âˆ‚Î· (a, x) âˆ‚Îº a - âˆ« x, âˆ« y, g (x, y) âˆ‚Î· (a, x) âˆ‚Îº a :=
  (Kernel.integral_fn_integral_sub id hf hg).trans <|
    integral_sub hf.integral_compProd hg.integral_compProd

theorem Kernel.integral_integral_sub' â¦ƒf g : Î² Ã— Î³ â†’ Eâ¦„ (hf : Integrable f ((Îº âŠ—â‚– Î·) a))
    (hg : Integrable g ((Îº âŠ—â‚– Î·) a)) :
    âˆ« x, âˆ« y, (f - g) (x, y) âˆ‚Î· (a, x) âˆ‚Îº a =
      âˆ« x, âˆ« y, f (x, y) âˆ‚Î· (a, x) âˆ‚Îº a - âˆ« x, âˆ« y, g (x, y) âˆ‚Î· (a, x) âˆ‚Îº a :=
  Kernel.integral_integral_sub hf hg

theorem Kernel.continuous_integral_integral :
    Continuous fun f : Î² Ã— Î³ â†’â‚[(Îº âŠ—â‚– Î·) a] E => âˆ« x, âˆ« y, f (x, y) âˆ‚Î· (a, x) âˆ‚Îº a := by
  rw [continuous_iff_continuousAt]; intro g
  refine
    tendsto_integral_of_L1 _ (L1.integrable_coeFn g).integral_compProd
      (Eventually.of_forall fun h => (L1.integrable_coeFn h).integral_compProd) ?_
  simp_rw [â† lintegral_fn_integral_sub (â€–Â·â€–â‚‘) (L1.integrable_coeFn _) (L1.integrable_coeFn g)]
  apply tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds _ (fun i => zero_le _) _
  Â· exact fun i => âˆ«â» x, âˆ«â» y, â€–i (x, y) - g (x, y)â€–â‚‘ âˆ‚Î· (a, x) âˆ‚Îº a
  swap; Â· exact fun i => lintegral_mono fun x => enorm_integral_le_lintegral_enorm _
  change
    Tendsto
      (fun i : Î² Ã— Î³ â†’â‚[(Îº âŠ—â‚– Î·) a] E => âˆ«â» x, âˆ«â» y : Î³, â€–i (x, y) - g (x, y)â€–â‚‘ âˆ‚Î· (a, x) âˆ‚Îº a)
      (ğ“ g) (ğ“ 0)
  have this (i : Lp (Î± := Î² Ã— Î³) E 1 (((Îº âŠ—â‚– Î·) a) : Measure (Î² Ã— Î³))) :
      Measurable fun z => â€–i z - g zâ€–â‚‘ :=
    ((Lp.stronglyMeasurable i).sub (Lp.stronglyMeasurable g)).enorm
  simp_rw [â† lintegral_compProd _ _ _ (this _), â† L1.ofReal_norm_sub_eq_lintegral, â† ofReal_zero]
  refine (continuous_ofReal.tendsto 0).comp ?_
  rw [â† tendsto_iff_norm_sub_tendsto_zero]
  exact tendsto_id

theorem integral_compProd :
    âˆ€ {f : Î² Ã— Î³ â†’ E} (_ : Integrable f ((Îº âŠ—â‚– Î·) a)),
      âˆ« z, f z âˆ‚(Îº âŠ—â‚– Î·) a = âˆ« x, âˆ« y, f (x, y) âˆ‚Î· (a, x) âˆ‚Îº a := by
  by_cases hE : CompleteSpace E; swap
  Â· simp [integral, hE]
  apply Integrable.induction
  Â· intro c s hs h2s
    simp_rw [integral_indicator hs, â† indicator_comp_right, Function.comp_def,
      integral_indicator (measurable_prodMk_left hs), MeasureTheory.setIntegral_const,
      integral_smul_const, measureReal_def]
    congr 1
    rw [integral_toReal]
    rotate_left
    Â· exact (Kernel.measurable_kernel_prodMk_left' hs _).aemeasurable
    Â· exact ae_kernel_lt_top a h2s.ne
    rw [Kernel.compProd_apply hs]
  Â· intro f g _ i_f i_g hf hg
    simp_rw [integral_add' i_f i_g, Kernel.integral_integral_add' i_f i_g, hf, hg]
  Â· exact isClosed_eq continuous_integral Kernel.continuous_integral_integral
  Â· intro f g hfg _ hf
    convert hf using 1
    Â· exact integral_congr_ae hfg.symm
    Â· apply integral_congr_ae
      filter_upwards [ae_ae_of_ae_compProd hfg] with x hfgx using
        integral_congr_ae (ae_eq_symm hfgx)

theorem setIntegral_compProd {f : Î² Ã— Î³ â†’ E} {s : Set Î²} {t : Set Î³} (hs : MeasurableSet s)
    (ht : MeasurableSet t) (hf : IntegrableOn f (s Ã—Ë¢ t) ((Îº âŠ—â‚– Î·) a)) :
    âˆ« z in s Ã—Ë¢ t, f z âˆ‚(Îº âŠ—â‚– Î·) a = âˆ« x in s, âˆ« y in t, f (x, y) âˆ‚Î· (a, x) âˆ‚Îº a := by
  -- Porting note: `compProd_restrict` needed some explicit arguments
  rw [â† Kernel.restrict_apply (Îº âŠ—â‚– Î·) (hs.prod ht), â† compProd_restrict hs ht, integral_compProd]
  Â· simp_rw [Kernel.restrict_apply]
  Â· rw [compProd_restrict, Kernel.restrict_apply]; exact hf

theorem setIntegral_compProd_univ_right (f : Î² Ã— Î³ â†’ E) {s : Set Î²} (hs : MeasurableSet s)
    (hf : IntegrableOn f (s Ã—Ë¢ univ) ((Îº âŠ—â‚– Î·) a)) :
    âˆ« z in s Ã—Ë¢ univ, f z âˆ‚(Îº âŠ—â‚– Î·) a = âˆ« x in s, âˆ« y, f (x, y) âˆ‚Î· (a, x) âˆ‚Îº a := by
  simp_rw [setIntegral_compProd hs MeasurableSet.univ hf, Measure.restrict_univ]

theorem setIntegral_compProd_univ_left (f : Î² Ã— Î³ â†’ E) {t : Set Î³} (ht : MeasurableSet t)
    (hf : IntegrableOn f (univ Ã—Ë¢ t) ((Îº âŠ—â‚– Î·) a)) :
    âˆ« z in univ Ã—Ë¢ t, f z âˆ‚(Îº âŠ—â‚– Î·) a = âˆ« x, âˆ« y in t, f (x, y) âˆ‚Î· (a, x) âˆ‚Îº a := by
  simp_rw [setIntegral_compProd MeasurableSet.univ ht hf, Measure.restrict_univ]

end compProd

section comp

variable {Îº : Kernel Î± Î²} {Î· : Kernel Î² Î³}

theorem _root_.MeasureTheory.AEStronglyMeasurable.integral_kernel_comp [NormedSpace â„ E]
    â¦ƒf : Î³ â†’ Eâ¦„ (hf : AEStronglyMeasurable f ((Î· âˆ˜â‚– Îº) a)) :
    AEStronglyMeasurable (fun x â†¦ âˆ« y, f y âˆ‚Î· x) (Îº a) :=
  âŸ¨fun x â†¦ âˆ« y, hf.mk f y âˆ‚Î· x, hf.stronglyMeasurable_mk.integral_kernel, by
    filter_upwards [ae_ae_of_ae_comp hf.ae_eq_mk] with _ hx using integral_congr_ae hxâŸ©

theorem _root_.MeasureTheory.AEStronglyMeasurable.comp {Î´ : Type*} [TopologicalSpace Î´]
    {f : Î³ â†’ Î´} (hf : AEStronglyMeasurable f ((Î· âˆ˜â‚– Îº) a)) :
    âˆ€áµ x âˆ‚Îº a, AEStronglyMeasurable f (Î· x) := by
  filter_upwards [ae_ae_of_ae_comp hf.ae_eq_mk] with x hx using
    âŸ¨hf.mk f, hf.stronglyMeasurable_mk, hxâŸ©

/-! ### Integrability with respect to composition -/

theorem hasFiniteIntegral_comp_iff â¦ƒf : Î³ â†’ Eâ¦„ (hf : StronglyMeasurable f) :
    HasFiniteIntegral f ((Î· âˆ˜â‚– Îº) a) â†”
    (âˆ€áµ x âˆ‚Îº a, HasFiniteIntegral f (Î· x)) âˆ§ HasFiniteIntegral (fun x â†¦ âˆ« y, â€–f yâ€– âˆ‚Î· x) (Îº a) := by
  simp_rw [hasFiniteIntegral_iff_enorm, lintegral_comp _ _ _ hf.enorm]
  simp_rw [integral_eq_lintegral_of_nonneg_ae (ae_of_all _ fun y â†¦ norm_nonneg _)
      hf.norm.aestronglyMeasurable, enorm_eq_ofReal toReal_nonneg, ofReal_norm_eq_enorm]
  have : âˆ€ {p q r : Prop} (_ : r â†’ p), (r â†” p âˆ§ q) â†” p â†’ (r â†” q) := fun h â†¦ by
    rw [â† and_congr_right_iff, and_iff_right_of_imp h]
  rw [this]
  Â· intro h
    rw [lintegral_congr_ae]
    filter_upwards [h] with x hx
    rw [ofReal_toReal]
    finiteness
  Â· exact fun h â†¦ ae_lt_top hf.enorm.lintegral_kernel h.ne

theorem hasFiniteIntegral_comp_iff' â¦ƒf : Î³ â†’ Eâ¦„ (hf : AEStronglyMeasurable f ((Î· âˆ˜â‚– Îº) a)) :
    HasFiniteIntegral f ((Î· âˆ˜â‚– Îº) a) â†”
    (âˆ€áµ x âˆ‚Îº a, HasFiniteIntegral f (Î· x)) âˆ§ HasFiniteIntegral (fun x â†¦ âˆ« y, â€–f yâ€– âˆ‚Î· x) (Îº a) := by
  rw [hasFiniteIntegral_congr hf.ae_eq_mk, hasFiniteIntegral_comp_iff hf.stronglyMeasurable_mk]
  refine and_congr (eventually_congr ?_) (hasFiniteIntegral_congr ?_)
  Â· filter_upwards [ae_ae_of_ae_comp hf.ae_eq_mk.symm] with _ hx using
      hasFiniteIntegral_congr hx
  Â· filter_upwards [ae_ae_of_ae_comp hf.ae_eq_mk.symm] with _ hx using
      integral_congr_ae (EventuallyEq.fun_comp hx _)

theorem integrable_comp_iff â¦ƒf : Î³ â†’ Eâ¦„ (hf : AEStronglyMeasurable f ((Î· âˆ˜â‚– Îº) a)) :
    Integrable f ((Î· âˆ˜â‚– Îº) a) â†”
    (âˆ€áµ y âˆ‚Îº a, Integrable f (Î· y)) âˆ§ Integrable (fun y â†¦ âˆ« z, â€–f zâ€– âˆ‚Î· y) (Îº a) := by
  simp only [Integrable, hf, hasFiniteIntegral_comp_iff' hf, true_and, eventually_and, hf.comp,
    hf.norm.integral_kernel_comp]

theorem _root_.MeasureTheory.Integrable.ae_of_comp â¦ƒf : Î³ â†’ Eâ¦„ (hf : Integrable f ((Î· âˆ˜â‚– Îº) a)) :
    âˆ€áµ x âˆ‚Îº a, Integrable f (Î· x) := ((integrable_comp_iff hf.1).1 hf).1

theorem _root_.MeasureTheory.Integrable.integral_norm_comp â¦ƒf : Î³ â†’ Eâ¦„
    (hf : Integrable f ((Î· âˆ˜â‚– Îº) a)) : Integrable (fun x â†¦ âˆ« y, â€–f yâ€– âˆ‚Î· x) (Îº a) :=
  ((integrable_comp_iff hf.1).1 hf).2

theorem _root_.MeasureTheory.Integrable.integral_comp [NormedSpace â„ E] â¦ƒf : Î³ â†’ Eâ¦„
    (hf : Integrable f ((Î· âˆ˜â‚– Îº) a)) : Integrable (fun x â†¦ âˆ« y, f y âˆ‚Î· x) (Îº a) :=
  Integrable.mono hf.integral_norm_comp hf.1.integral_kernel_comp <|
    ae_of_all _ fun _ â†¦ (norm_integral_le_integral_norm _).trans_eq
    (norm_of_nonneg <| integral_nonneg_of_ae <| ae_of_all _ fun _ â†¦ norm_nonneg _).symm

/-! ### Bochner integral with respect to the composition -/

variable [NormedSpace â„ E] {E' : Type*} [NormedAddCommGroup E'] [NormedSpace â„ E']

namespace Kernel

theorem integral_fn_integral_add_comp â¦ƒf g : Î³ â†’ Eâ¦„ (F : E â†’ E')
    (hf : Integrable f ((Î· âˆ˜â‚– Îº) a)) (hg : Integrable g ((Î· âˆ˜â‚– Îº) a)) :
    âˆ« x, F (âˆ« y, f y + g y âˆ‚Î· x) âˆ‚Îº a = âˆ« x, F (âˆ« y, f y âˆ‚Î· x + âˆ« y, g y âˆ‚Î· x) âˆ‚Îº a := by
  refine integral_congr_ae ?_
  filter_upwards [hf.ae_of_comp, hg.ae_of_comp] with _ h2f h2g
  simp [integral_add h2f h2g]

theorem integral_fn_integral_sub_comp â¦ƒf g : Î³ â†’ Eâ¦„ (F : E â†’ E')
    (hf : Integrable f ((Î· âˆ˜â‚– Îº) a)) (hg : Integrable g ((Î· âˆ˜â‚– Îº) a)) :
    âˆ« x, F (âˆ« y, f y - g y âˆ‚Î· x) âˆ‚Îº a = âˆ« x, F (âˆ« y, f y âˆ‚Î· x - âˆ« y, g y âˆ‚Î· x) âˆ‚Îº a := by
  refine integral_congr_ae ?_
  filter_upwards [hf.ae_of_comp, hg.ae_of_comp] with _ h2f h2g
  simp [integral_sub h2f h2g]

theorem lintegral_fn_integral_sub_comp â¦ƒf g : Î³ â†’ Eâ¦„ (F : E â†’ â„â‰¥0âˆ)
    (hf : Integrable f ((Î· âˆ˜â‚– Îº) a)) (hg : Integrable g ((Î· âˆ˜â‚– Îº) a)) :
    âˆ«â» x, F (âˆ« y, f y - g y âˆ‚Î· x) âˆ‚Îº a = âˆ«â» x, F (âˆ« y, f y âˆ‚Î· x - âˆ« y, g y âˆ‚Î· x) âˆ‚Îº a := by
  refine lintegral_congr_ae ?_
  filter_upwards [hf.ae_of_comp, hg.ae_of_comp] with _ h2f h2g
  simp [integral_sub h2f h2g]

theorem integral_integral_add_comp â¦ƒf g : Î³ â†’ Eâ¦„ (hf : Integrable f ((Î· âˆ˜â‚– Îº) a))
    (hg : Integrable g ((Î· âˆ˜â‚– Îº) a)) :
    âˆ« x, âˆ« y, f y + g y âˆ‚Î· x âˆ‚Îº a = âˆ« x, âˆ« y, f y âˆ‚Î· x âˆ‚Îº a + âˆ« x, âˆ« y, g y âˆ‚Î· x âˆ‚Îº a :=
  (integral_fn_integral_add_comp id hf hg).trans <| integral_add hf.integral_comp hg.integral_comp

theorem integral_integral_add'_comp â¦ƒf g : Î³ â†’ Eâ¦„ (hf : Integrable f ((Î· âˆ˜â‚– Îº) a))
    (hg : Integrable g ((Î· âˆ˜â‚– Îº) a)) :
    âˆ« x, âˆ« y, (f + g) y âˆ‚Î· x âˆ‚Îº a = âˆ« x, âˆ« y, f y âˆ‚Î· x âˆ‚Îº a + âˆ« x, âˆ« y, g y âˆ‚Î· x âˆ‚Îº a :=
  integral_integral_add_comp hf hg

theorem integral_integral_sub_comp â¦ƒf g : Î³ â†’ Eâ¦„ (hf : Integrable f ((Î· âˆ˜â‚– Îº) a))
    (hg : Integrable g ((Î· âˆ˜â‚– Îº) a)) :
    âˆ« x, âˆ« y, f y - g y âˆ‚Î· x âˆ‚Îº a = âˆ« x, âˆ« y, f y âˆ‚Î· x âˆ‚Îº a - âˆ« x, âˆ« y, g y âˆ‚Î· x âˆ‚Îº a :=
  (integral_fn_integral_sub_comp id hf hg).trans <| integral_sub hf.integral_comp hg.integral_comp

theorem integral_integral_sub'_comp â¦ƒf g : Î³ â†’ Eâ¦„ (hf : Integrable f ((Î· âˆ˜â‚– Îº) a))
    (hg : Integrable g ((Î· âˆ˜â‚– Îº) a)) :
    âˆ« x, âˆ« y, (f - g) y âˆ‚Î· x âˆ‚Îº a = âˆ« x, âˆ« y, f y âˆ‚Î· x âˆ‚Îº a - âˆ« x, âˆ« y, g y âˆ‚Î· x âˆ‚Îº a :=
  integral_integral_sub_comp hf hg

theorem continuous_integral_integral_comp :
    Continuous fun f : Î³ â†’â‚[(Î· âˆ˜â‚– Îº) a] E â†¦ âˆ« x, âˆ« y, f y âˆ‚Î· x âˆ‚Îº a := by
  refine continuous_iff_continuousAt.2 fun g â†¦ ?_
  refine tendsto_integral_of_L1 _ (L1.integrable_coeFn g).integral_comp
      (Eventually.of_forall fun h â†¦ (L1.integrable_coeFn h).integral_comp) ?_
  simp_rw [â† lintegral_fn_integral_sub_comp (â€–Â·â€–â‚‘) (L1.integrable_coeFn _) (L1.integrable_coeFn g)]
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le
    (h := fun i â†¦ âˆ«â» x, âˆ«â» y, â€–i y - g yâ€–â‚‘ âˆ‚Î· x âˆ‚Îº a)
    tendsto_const_nhds ?_ (fun _ â†¦ zero_le _) ?_
  swap; Â· exact fun _ â†¦ lintegral_mono fun _ â†¦ enorm_integral_le_lintegral_enorm _
  have (i : Î³ â†’â‚[(Î· âˆ˜â‚– Îº) a] E) : Measurable fun z â†¦ â€–i z - g zâ€–â‚‘ :=
    ((Lp.stronglyMeasurable i).sub (Lp.stronglyMeasurable g)).enorm
  simp_rw [â† lintegral_comp _ _ _ (this _), â† L1.ofReal_norm_sub_eq_lintegral, â† ofReal_zero]
  exact (continuous_ofReal.tendsto 0).comp (tendsto_iff_norm_sub_tendsto_zero.1 tendsto_id)

theorem integral_comp : âˆ€ {f : Î³ â†’ E} (_ : Integrable f ((Î· âˆ˜â‚– Îº) a)),
    âˆ« z, f z âˆ‚(Î· âˆ˜â‚– Îº) a = âˆ« x, âˆ« y, f y âˆ‚Î· x âˆ‚Îº a := by
  by_cases hE : CompleteSpace E; swap
  Â· simp [integral, hE]
  apply Integrable.induction
  Â· intro c s hs ms
    simp_rw [integral_indicator hs, MeasureTheory.setIntegral_const, integral_smul_const,
      measureReal_def]
    congr
    rw [integral_toReal, Kernel.comp_apply' _ _ _ hs]
    Â· exact (Kernel.measurable_coe _ hs).aemeasurable
    Â· exact ae_lt_top_of_comp_ne_top a ms.ne
  Â· rintro f g - i_f i_g hf hg
    simp_rw [integral_add' i_f i_g, integral_integral_add'_comp i_f i_g, hf, hg]
  Â· exact isClosed_eq continuous_integral Kernel.continuous_integral_integral_comp
  Â· rintro f g hfg - hf
    convert hf using 1
    Â· exact integral_congr_ae hfg.symm
    Â· apply integral_congr_ae
      filter_upwards [ae_ae_of_ae_comp hfg] with x hfgx using integral_congr_ae (ae_eq_symm hfgx)

theorem setIntegral_comp {f : Î³ â†’ E} {s : Set Î³} (hs : MeasurableSet s)
    (hf : IntegrableOn f s ((Î· âˆ˜â‚– Îº) a)) :
    âˆ« z in s, f z âˆ‚(Î· âˆ˜â‚– Îº) a = âˆ« x, âˆ« y in s, f y âˆ‚Î· x âˆ‚Îº a := by
  rw [â† restrict_apply (Î· âˆ˜â‚– Îº) hs, â† comp_restrict hs, integral_comp]
  Â· simp_rw [restrict_apply]
  Â· rwa [comp_restrict, restrict_apply]

end Kernel

end comp

end ProbabilityTheory

namespace MeasureTheory

namespace Measure

variable {Î± Î² E : Type*} {mÎ± : MeasurableSpace Î±} {mÎ² : MeasurableSpace Î²}
  [NormedAddCommGroup E] {a : Î±} {Îº : Kernel Î± Î²} {Î¼ : Measure Î±} {f : Î² â†’ E}

section Integral

lemma _root_.MeasureTheory.AEStronglyMeasurable.ae_of_compProd [SFinite Î¼] [IsSFiniteKernel Îº]
    {E : Type*} [NormedAddCommGroup E] {f : Î± â†’ Î² â†’ E}
    (hf : AEStronglyMeasurable f.uncurry (Î¼ âŠ—â‚˜ Îº)) :
    âˆ€áµ x âˆ‚Î¼, AEStronglyMeasurable (f x) (Îº x) := by
  simpa using hf.compProd_mk_left

lemma integrable_compProd_iff [SFinite Î¼] [IsSFiniteKernel Îº] {E : Type*} [NormedAddCommGroup E]
    {f : Î± Ã— Î² â†’ E} (hf : AEStronglyMeasurable f (Î¼ âŠ—â‚˜ Îº)) :
    Integrable f (Î¼ âŠ—â‚˜ Îº) â†”
      (âˆ€áµ x âˆ‚Î¼, Integrable (fun y => f (x, y)) (Îº x)) âˆ§
        Integrable (fun x => âˆ« y, â€–f (x, y)â€– âˆ‚(Îº x)) Î¼ := by
  simp_rw [Measure.compProd, ProbabilityTheory.integrable_compProd_iff hf, Kernel.prodMkLeft_apply,
    Kernel.const_apply]

lemma integral_compProd [SFinite Î¼] [IsSFiniteKernel Îº] {E : Type*}
    [NormedAddCommGroup E] [NormedSpace â„ E]
    {f : Î± Ã— Î² â†’ E} (hf : Integrable f (Î¼ âŠ—â‚˜ Îº)) :
    âˆ« x, f x âˆ‚(Î¼ âŠ—â‚˜ Îº) = âˆ« a, âˆ« b, f (a, b) âˆ‚(Îº a) âˆ‚Î¼ := by
  rw [Measure.compProd, ProbabilityTheory.integral_compProd hf]
  simp

lemma setIntegral_compProd [SFinite Î¼] [IsSFiniteKernel Îº] {E : Type*}
    [NormedAddCommGroup E] [NormedSpace â„ E]
    {s : Set Î±} (hs : MeasurableSet s) {t : Set Î²} (ht : MeasurableSet t)
    {f : Î± Ã— Î² â†’ E} (hf : IntegrableOn f (s Ã—Ë¢ t) (Î¼ âŠ—â‚˜ Îº)) :
    âˆ« x in s Ã—Ë¢ t, f x âˆ‚(Î¼ âŠ—â‚˜ Îº) = âˆ« a in s, âˆ« b in t, f (a, b) âˆ‚(Îº a) âˆ‚Î¼ := by
  rw [Measure.compProd, ProbabilityTheory.setIntegral_compProd hs ht hf]
  simp

end Integral

section Integrable

lemma integrable_compProd_snd_iff [SFinite Î¼] [IsSFiniteKernel Îº]
    (hf : AEStronglyMeasurable f (Îº âˆ˜â‚˜ Î¼)) :
    Integrable (fun p â†¦ f p.2) (Î¼ âŠ—â‚˜ Îº) â†” Integrable f (Îº âˆ˜â‚˜ Î¼) := by
  rw [â† Measure.snd_compProd, Measure.snd, integrable_map_measure _ measurable_snd.aemeasurable,
    Function.comp_def]
  rwa [â† Measure.snd, Measure.snd_compProd]

lemma ae_integrable_of_integrable_comp (h_int : Integrable f (Îº âˆ˜â‚˜ Î¼)) :
    âˆ€áµ x âˆ‚Î¼, Integrable f (Îº x) := by
  rw [Measure.comp_eq_comp_const_apply, integrable_comp_iff h_int.1] at h_int
  exact h_int.1

lemma integrable_integral_norm_of_integrable_comp (h_int : Integrable f (Îº âˆ˜â‚˜ Î¼)) :
    Integrable (fun x â†¦ âˆ« y, â€–f yâ€– âˆ‚Îº x) Î¼ := by
  rw [Measure.comp_eq_comp_const_apply, integrable_comp_iff h_int.1] at h_int
  exact h_int.2

end Integrable

end Measure

end MeasureTheory
