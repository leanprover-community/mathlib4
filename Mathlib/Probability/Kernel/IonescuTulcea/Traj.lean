/-
Copyright (c) 2024 Etienne Marion. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Etienne Marion
-/
import Mathlib.MeasureTheory.Measure.ProbabilityMeasure
import Mathlib.MeasureTheory.Constructions.ProjectiveFamilyContent
import Mathlib.MeasureTheory.OuterMeasure.OfAddContent
import Mathlib.Probability.Kernel.Composition.IntegralCompProd
import Mathlib.Probability.Kernel.Composition.MeasureComp
import Mathlib.Probability.Kernel.Composition.MeasureCompProd
import Mathlib.Probability.Kernel.Integral
import Mathlib.Probability.Kernel.IonescuTulcea.PTraj
import Mathlib.Probability.Process.Filtration

/-!
# Ionescu-Tulcea theorem

This file proves the *Ionescu-Tulcea theorem*. The idea of the statement is as follows:
consider a family of kernels `Îº : (n : â„•) â†’ Kernel (Î  i : Iic n, X i) (X (n + 1))`.
One can interpret `Îº n` as a kernel which takes as an input the trajectory of a point started in
`X 0` and moving `X 0 â†’ X 1 â†’ X 2 â†’ ... â†’ X n` and which outputs the distribution of the next
position of the point in `X (n + 1)`. If `a b : â„•` and `a < b`, we can compose the kernels,
and `Îº a âŠ—â‚– Îº (a + 1) âŠ—â‚– ... âŠ—â‚– Îº b` will take the trajectory up to time `a` as input and outputs
the distrbution of the trajectory on `X (a + 1) Ã— ... Ã— X (b + 1)`.

The Ionescu-Tulcea theorem tells us that these compositions can be extended into a
`Kernel (Î  i : Iic a, X i) (Î  n â‰¥ a, X n)` which given the trajectory up to time `a` outputs
the distribution of the infinite trajectory started in `X (a + 1)`. In other words this theorem
makes sense of composing infinitely many kernels together.

In this file we construct this "limit" kernel given the family `Îº`. More precisely, for any `a : â„•`,
we construct the kernel `traj Îº a : Kernel (Î  i : Iic a, X i) (Î  n, X n)`, which takes as input
the trajectory in `X 0 Ã— ... Ã— X a` and outputs the distribution of the whole trajectory. The name
`traj` thus stands for "trajectory". We build a kernel with output in `Î  n, X n` instead of
`Î  i : Iic a, X i` to make manipulations easier. The first coordinates are deterministic.

We also provide to compute integrals against `traj Îº a` and an expression for the conditional
expectation.

## Main definition

* `traj Îº a`: a kernel from `Î  i : Iic a, X i` to `Î  n, X n` which takes as input a trajectory
  up to time `a` ad outputs the distribution of the trajectory obtained by iterating the kernels
  `Îº`. Its existence is given by the Ionescu-Tulcea theorem.

## Main statements

* `eq_traj`: Uniqueness of `traj`: to check that `Î· = traj Îº a` it is enough to show that
  the restriction of `Î·` to variables `â‰¤ b` is `ptraj Îº a b`.
* `traj_comp_ptraj`: Given the distribution up to tome `a`, `ptraj Îº a b` gives the distribution of
  the trajectory up to time `b`, and composing this with `traj Îº b` gives the distribution
  of the whole trajectory.
* `condExp_traj`: If `a â‰¤ b`, the conditional expectation of `f` with respect to `traj Îº a`
  given the information up to time `b` is obtained by integrating `f` against `traj Îº b`.

## Implementation notes

The kernel `traj Îº a` is built using the CarathÃ©odory extension theorem. First we build a projective
family of measures using `inducedFamily` and `ptraj Îº a`. Then we build an
`MeasureTheory.AddContent` on `MeasureTheory.measurableCylinders` called `trajContent` using
`projectiveFamilyContent`. Finally we prove `trajContent_tendsto_zero` which implies the
`Ïƒ`-additivity of the content, allowing to turn it into a measure.

## References

We follow the proof of Theorem 8.24 in
[O. Kallenberg, *Foundations of Modern Probability*][kallenberg2021]. For a more detailed proof
in the case of constant kernels (i.e. measures),
see Proposition 10.6.1 in [D. L. Cohn, *Measure Theory*][cohn2013measure].

## Tags

Ionescu-Tulcea theorem
-/

open Filter Finset Function MeasurableEquiv MeasurableSpace MeasureTheory Preorder ProbabilityTheory

open scoped ENNReal Topology

variable {X : â„• â†’ Type*}

/-- Gluing `Iic a` and `Ioi a` into `â„•`, version as a measurable equivalence
on dependent functions. -/
def MeasurableEquiv.IicProdIoi [âˆ€ n, MeasurableSpace (X n)] (a : â„•) :
    ((Î  i : Iic a, X i) Ã— ((i : Set.Ioi a) â†’ X i)) â‰ƒáµ (Î  n, X n) where
  toFun := fun x i â†¦ if hi : i â‰¤ a
    then x.1 âŸ¨i, mem_Iic.2 hiâŸ©
    else x.2 âŸ¨i, Set.mem_Ioi.2 (not_le.1 hi)âŸ©
  invFun := fun x â†¦ (fun i â†¦ x i, fun i â†¦ x i)
  left_inv := fun x â†¦ by
    ext i
    Â· simp [mem_Iic.1 i.2]
    Â· simp [not_le.2 <| Set.mem_Ioi.1 i.2]
  right_inv := fun x â†¦ by simp
  measurable_toFun := by
    refine measurable_pi_lambda _ (fun i â†¦ ?_)
    by_cases hi : i â‰¤ a <;> simp only [Equiv.coe_fn_mk, hi, â†“reduceDIte]
    Â· exact measurable_fst.eval
    Â· exact measurable_snd.eval
  measurable_invFun := Measurable.prod_mk (measurable_restrict _) (Set.measurable_restrict _)

section castLemmas

private lemma Iic_pi_eq {a b : â„•} (h : a = b) :
    (Î  i : Iic a, X i) = (Î  i : Iic b, X i) := by cases h; rfl

private lemma cast_pi {s t : Set â„•} (h : s = t) (x : (i : s) â†’ X i) (i : t) :
    cast (congrArg (fun u : Set â„• â†¦ (Î  i : u, X i)) h) x i = x âŸ¨i.1, h.symm â–¸ i.2âŸ© := by
  cases h; rfl

variable [âˆ€ n, MeasurableSpace (X n)]

private lemma measure_cast {a b : â„•} (h : a = b) (Î¼ : (n : â„•) â†’ Measure (Î  i : Iic n, X i)) :
    (Î¼ a).map (cast (Iic_pi_eq h)) = Î¼ b := by
  cases h
  exact Measure.map_id

private lemma heq_measurableSpace_Iic_pi {a b : â„•} (h : a = b) :
    HEq (inferInstance : MeasurableSpace (Î  i : Iic a, X i))
    (inferInstance : MeasurableSpace (Î  i : Iic b, X i)) := by cases h; rfl

end castLemmas

section iterateInduction

/-- This function takes as input a tuple `(x_â‚€, ..., x_â‚)` and `ind` a function which
given `(y_â‚€, ...,y_â‚™)` outputs `x_{n+1} : X (n + 1)`, and it builds an element of `Î  n, X n`
by starting with `(x_â‚€, ..., x_â‚)` and then iterating `ind`. -/
def iterateInduction {a : â„•} (x : Î  i : Iic a, X i)
    (ind : (n : â„•) â†’ (Î  i : Iic n, X i) â†’ X (n + 1)) : Î  n, X n
  | 0 => x âŸ¨0, mem_Iic.2 <| zero_le aâŸ©
  | k + 1 => if h : k + 1 â‰¤ a
      then x âŸ¨k + 1, mem_Iic.2 hâŸ©
      else ind k (fun i â†¦ iterateInduction x ind i)
  decreasing_by exact Nat.lt_succ.2 (mem_Iic.1 i.2)

lemma frestrictLe_iterateInduction {a : â„•} (x : Î  i : Iic a, X i)
    (ind : (n : â„•) â†’ (Î  i : Iic n, X i) â†’ X (n + 1)) :
    frestrictLe a (iterateInduction x ind) = x := by
  ext i
  simp only [frestrictLe_apply]
  obtain âŸ¨(zero | j), hjâŸ© := i <;> rw [iterateInduction]
  rw [dif_pos (mem_Iic.1 hj)]

end iterateInduction

variable [âˆ€ n, MeasurableSpace (X n)]

section ProjectiveFamily

namespace MeasureTheory

/-! ### Projective families indexed by `Finset â„•` -/

variable {Î¼ : (n : â„•) â†’ Measure (Î  i : Iic n, X i)}

/-- To check that a measure `Î½` is the projective limit of a projective family of measures indexed
by `Finset â„•`, it is enough to check on intervals of the form `Iic n`, where `n` is larger than
a given integer. -/
theorem isProjectiveLimit_nat_iff' {Î¼ : (I : Finset â„•) â†’ Measure (Î  i : I, X i)}
    (hÎ¼ : IsProjectiveMeasureFamily Î¼) (Î½ : Measure (Î  n, X n)) (a : â„•) :
    IsProjectiveLimit Î½ Î¼ â†” âˆ€ â¦ƒnâ¦„, n â‰¥ a â†’ Î½.map (frestrictLe n) = Î¼ (Iic n) := by
  refine âŸ¨fun h n _ â†¦ h (Iic n), fun h I â†¦ ?_âŸ©
  have := (I.subset_Iic_sup_id.trans (Iic_subset_Iic.2 (le_max_left (I.sup id) a)))
  rw [â† restrictâ‚‚_comp_restrict this, â† Measure.map_map, â† frestrictLe, h (le_max_right _ _), â† hÎ¼]
  all_goals fun_prop

/-- To check that a measure `Î½` is the projective limit of a projective family of measures indexed
by `Finset â„•`, it is enough to check on intervals of the form `Iic n`. -/
theorem isProjectiveLimit_nat_iff {Î¼ : (I : Finset â„•) â†’ Measure (Î  i : I, X i)}
    (hÎ¼ : IsProjectiveMeasureFamily Î¼) (Î½ : Measure (Î  n, X n)) :
    IsProjectiveLimit Î½ Î¼ â†” âˆ€ n, Î½.map (frestrictLe n) = Î¼ (Iic n) := by
  rw [isProjectiveLimit_nat_iff' hÎ¼ _ 0]
  simp

variable (Î¼ : (n : â„•) â†’ Measure (Î  i : Iic n, X i))

/-- Given a family of measures `Î¼ : (n : â„•) â†’ Measure (Î  i : Iic n, X i)`, we can define a family
of measures indexed by `Finset â„•` by projecting the measures. -/
noncomputable def inducedFamily (S : Finset â„•) : Measure ((k : S) â†’ X k) :=
    (Î¼ (S.sup id)).map (restrictâ‚‚ S.subset_Iic_sup_id)

instance [âˆ€ n, SFinite (Î¼ n)] (I : Finset â„•) :
    SFinite (inducedFamily Î¼ I) := by rw [inducedFamily]; infer_instance

instance [âˆ€ n, IsFiniteMeasure (Î¼ n)] (I : Finset â„•) :
    IsFiniteMeasure (inducedFamily Î¼ I) := by rw [inducedFamily]; infer_instance

instance [âˆ€ n, IsZeroOrProbabilityMeasure (Î¼ n)] (I : Finset â„•) :
    IsZeroOrProbabilityMeasure (inducedFamily Î¼ I) := by rw [inducedFamily]; infer_instance

instance [âˆ€ n, IsProbabilityMeasure (Î¼ n)] (I : Finset â„•) :
    IsProbabilityMeasure (inducedFamily Î¼ I) := by
  rw [inducedFamily]
  exact isProbabilityMeasure_map (measurable_restrictâ‚‚ _).aemeasurable

/-- Given a family of measures `Î¼ : (n : â„•) â†’ Measure (Î  i : Iic n, X i)`, the induced family
equals `Î¼` over the intervals `Iic n`. -/
theorem inducedFamily_Iic (n : â„•) : inducedFamily Î¼ (Iic n) = Î¼ n := by
  rw [inducedFamily, â† measure_cast (sup_Iic n) Î¼]
  congr with x i
  rw [restrictâ‚‚, cast_pi (by rw [sup_Iic n])]

/-- Given a family of measures `Î¼ : (n : â„•) â†’ Measure (Î  i : Iic n, X i)`, the induced family
will be projective only if `Î¼` is projective, in the sense that if `a â‰¤ b`, then projecting
`Î¼ b` gives `Î¼ a`. -/
theorem isProjectiveMeasureFamily_inducedFamily
    (h : âˆ€ a b : â„•, âˆ€ hab : a â‰¤ b, (Î¼ b).map (frestrictLeâ‚‚ hab) = Î¼ a) :
    IsProjectiveMeasureFamily (inducedFamily Î¼) := by
  intro I J hJI
  have sls : J.sup id â‰¤ I.sup id := sup_mono hJI
  simp only [inducedFamily]
  rw [Measure.map_map, restrictâ‚‚_comp_restrictâ‚‚,
    â† restrictâ‚‚_comp_restrictâ‚‚ J.subset_Iic_sup_id (Iic_subset_Iic.2 sls), â† Measure.map_map,
    â† frestrictLeâ‚‚, h (J.sup id) (I.sup id) sls]
  all_goals fun_prop

end MeasureTheory

end ProjectiveFamily

variable (Îº : (n : â„•) â†’ Kernel (Î  i : Iic n, X i) (X (n + 1))) [âˆ€ n, IsMarkovKernel (Îº n)]

namespace ProbabilityTheory.Kernel

section definition

/-! ### Definition and basic properties of `traj` -/

lemma isProjectiveMeasureFamily_ptraj {a : â„•} (xâ‚€ : Î  i : Iic a, X i) :
    IsProjectiveMeasureFamily (inducedFamily (fun b â†¦ ptraj Îº a b xâ‚€)) :=
  isProjectiveMeasureFamily_inducedFamily _ (fun _ _ â†¦ ptraj_map_frestrictLeâ‚‚_apply (Îº := Îº) xâ‚€)

/-- Given a family of kernels `Îº : (n : â„•) â†’ Kernel (Î  i : Iic n, X i) (X (n + 1))`, and the
trajectory up to time `a` we can construct an additive content over cylinders. It corresponds
to composing the kernels, starting at time `a + 1`. -/
noncomputable def trajContent {a : â„•} (xâ‚€ : Î  i : Iic a, X i) :
    AddContent (measurableCylinders X) :=
  projectiveFamilyContent (isProjectiveMeasureFamily_ptraj Îº xâ‚€)

/-- The `trajContent Îº xâ‚€` of a cylinder indexed by first coordinates is given by `ptraj`. -/
theorem trajContent_cylinder {a b : â„•} (xâ‚€ : Î  i : Iic a, X i)
    {S : Set (Î  i : Iic b, X i)} (mS : MeasurableSet S) :
    trajContent Îº xâ‚€ (cylinder _ S) = ptraj Îº a b xâ‚€ S := by
  rw [trajContent, projectiveFamilyContent_cylinder _ mS, inducedFamily_Iic]

/-- The `trajContent` of a cylinder is equal to the integral of its indicator function against
`ptraj`. -/
theorem trajContent_eq_lmarginalPTraj {b : â„•} {S : Set (Î  i : Iic b, X i)}
    (mS : MeasurableSet S) (xâ‚€ : Î  n, X n) (a : â„•) :
    trajContent Îº (frestrictLe a xâ‚€) (cylinder _ S) =
      lmarginalPTraj Îº a b ((cylinder _ S).indicator 1) xâ‚€ := by
  rw [trajContent_cylinder _ _ mS, â† lintegral_indicator_one mS, lmarginalPTraj]
  congr with x
  apply Set.indicator_const_eq_indicator_const
  rw [mem_cylinder]
  congrm (fun i â†¦ ?_) âˆˆ S
  simp [updateFinset, i.2]

/-- The cylinders of a product space indexed by `â„•` can be seen as depending on the first
coordinates. -/
theorem cylinders_nat :
    measurableCylinders X = â‹ƒ (a) (S) (_ : MeasurableSet S), {cylinder (Iic a) S} := by
  ext s
  simp only [mem_measurableCylinders, exists_prop, Set.mem_iUnion, mem_singleton]
  refine âŸ¨?_, fun âŸ¨N, S, mS, s_eqâŸ© â†¦ âŸ¨Iic N, S, mS, s_eqâŸ©âŸ©
  rintro âŸ¨t, S, mS, rflâŸ©
  refine âŸ¨t.sup id, restrictâ‚‚ t.subset_Iic_sup_id â»Â¹' S, measurable_restrictâ‚‚ _ mS, ?_âŸ©
  unfold cylinder
  rw [â† Set.preimage_comp, restrictâ‚‚_comp_restrict]
  exact Set.mem_singleton _

variable {Îº} in
lemma trajContent_ne_top {a : â„•} {x : Î  i : Iic a, X i} {s : Set (Î  n, X n)} :
    trajContent Îº x s â‰  âˆ :=
  projectiveFamilyContent_ne_top (isProjectiveMeasureFamily_ptraj Îº x)

/-- This is an auxiliary result for `trajContent_tendsto_zero`. Consider `f` a sequence of bounded
measurable functions such that `f n` depends only on the first coordinates up to `a n`.
Assume that when integrating `f n` against `ptraj (k + 1) (a n)`, one gets a non-increasing
sequence of functions wich converges to `l`.
Assume then that there exists `Îµ` and `y : Î  i : Iic k, X i` such that
when integrating `f n` against `ptraj k (a n) y`, you get something at least
`Îµ` for all `n`. Then there exists `z` such that this remains true when integrating
`f` against `ptraj (k + 1) (a n) (update y (k + 1) z)`. -/
theorem le_lmarginalPTraj_succ {f : â„• â†’ (Î  n, X n) â†’ â„â‰¥0âˆ} {a : â„• â†’ â„•}
    (hcte : âˆ€ n, DependsOn (f n) (Iic (a n))) (mf : âˆ€ n, Measurable (f n))
    {bound : â„â‰¥0âˆ} (fin_bound : bound â‰  âˆ) (le_bound : âˆ€ n x, f n x â‰¤ bound) {k : â„•}
    (anti : âˆ€ x, Antitone (fun n â†¦ lmarginalPTraj Îº (k + 1) (a n) (f n) x))
    {l : (Î  n, X n) â†’ â„â‰¥0âˆ}
    (htendsto : âˆ€ x, Tendsto (fun n â†¦ lmarginalPTraj Îº (k + 1) (a n) (f n) x) atTop (ğ“ (l x)))
    (Îµ : â„â‰¥0âˆ) (y : Î  i : Iic k, X i)
    (hpos : âˆ€ x n, Îµ â‰¤ lmarginalPTraj Îº k (a n) (f n) (updateFinset x _ y)) :
    âˆƒ z, âˆ€ x n,
      Îµ â‰¤ lmarginalPTraj Îº (k + 1) (a n) (f n) (update (updateFinset x _ y) (k + 1) z) := by
  have _ n : Nonempty (X n) := by
    induction n using Nat.case_strong_induction_on with
    | hz => exact âŸ¨y âŸ¨0, mem_Iic.2 (zero_le _)âŸ©âŸ©
    | hi m hm =>
      have : Nonempty (Î  i : Iic m, X i) :=
        âŸ¨fun i â†¦ @Classical.ofNonempty _ (hm i.1 (mem_Iic.1 i.2))âŸ©
      exact ProbabilityMeasure.nonempty âŸ¨Îº m Classical.ofNonempty, inferInstanceâŸ©
  -- `Fâ‚™` is the integral of `fâ‚™` from time `k + 1` to `aâ‚™`.
  let F n : (Î  n, X n) â†’ â„â‰¥0âˆ := lmarginalPTraj Îº (k + 1) (a n) (f n)
  -- `Fâ‚™` converges to `l` by hypothesis.
  have tendstoF x : Tendsto (F Â· x) atTop (ğ“ (l x)) := htendsto x
  -- Integrating `fâ‚™` between time `k` and `aâ‚™` is the same as integrating
  -- `Fâ‚™` between time `k` and time `k + 1`.
  have f_eq x n : lmarginalPTraj Îº k (a n) (f n) x = lmarginalPTraj Îº k (k + 1) (F n) x := by
    simp_rw [F]
    obtain h | h | h := lt_trichotomy (k + 1) (a n)
    Â· rw [â† lmarginalPTraj_self k.le_succ h.le (mf n)]
    Â· rw [â† h, lmarginalPTraj_le _ le_rfl (mf n)]
    Â· rw [lmarginalPTraj_le _ _ (mf n), (hcte n).lmarginalPTraj_le _ (mf n),
        (hcte n).lmarginalPTraj_le _ (mf n)]
      all_goals omega
  -- `F` is also a bounded sequence.
  have F_le n x : F n x â‰¤ bound := by
    simpa [F, lmarginalPTraj] using lintegral_le_const (ae_of_all _ fun z â†¦ le_bound _ _)
  -- By dominated convergence, the integral of `fâ‚™` between time `k` and time `a n` converges
  -- to the integral of `l` between time `k` and time `k + 1`.
  have tendsto_int x : Tendsto (fun n â†¦ lmarginalPTraj Îº k (a n) (f n) x) atTop
      (ğ“ (lmarginalPTraj Îº k (k + 1) l x)) := by
    simp_rw [f_eq, lmarginalPTraj]
    exact tendsto_lintegral_of_dominated_convergence (fun _ â†¦ bound)
      (fun n â†¦ (measurable_lmarginalPTraj _ _ (mf n)).comp measurable_updateFinset)
      (fun n â†¦ Eventually.of_forall <| fun y â†¦ F_le n _)
      (by simp [fin_bound]) (Eventually.of_forall (fun _ â†¦ tendstoF _))
  -- By hypothesis, we have `Îµ â‰¤ lmarginalPTraj Îº k (k + 1) (F n) (updateFinset x _ y)`,
  -- so this is also true for `l`.
  have Îµ_le_lint x : Îµ â‰¤ lmarginalPTraj Îº k (k + 1) l (updateFinset x _ y) :=
    ge_of_tendsto (tendsto_int _) (by simp [hpos])
  let x_ : Î  n, X n := Classical.ofNonempty
  -- We now have that the integral of `l` with respect to a probability measure is greater than `Îµ`,
  -- therefore there exists `x` such that `Îµ â‰¤ l(y, x)`.
  obtain âŸ¨x, hxâŸ© : âˆƒ x, Îµ â‰¤ l (update (updateFinset x_ _ y) (k + 1) x) := by
    have : âˆ«â» x, l (update (updateFinset x_ _ y) (k + 1) x) âˆ‚(Îº k y) â‰  âˆ :=
      ne_top_of_le_ne_top fin_bound <| lintegral_le_const <| ae_of_all _
        fun y â†¦ le_of_tendsto' (tendstoF _) <| fun _ â†¦ F_le _ _
    obtain âŸ¨x, hxâŸ© := exists_lintegral_le this
    refine âŸ¨x, (Îµ_le_lint x_).trans ?_âŸ©
    rwa [lmarginalPTraj_succ, frestrictLe_updateFinset]
    exact ENNReal.measurable_of_tendsto (by fun_prop) (tendsto_pi_nhds.2 htendsto)
  refine âŸ¨x, fun x' n â†¦ ?_âŸ©
  -- As `F` is a non-increasing sequence, we have `Îµ â‰¤ Fâ‚™(y, x)` for any `n`.
  have := le_trans hx ((anti _).le_of_tendsto (tendstoF _) n)
  -- This part below is just to say that this is true for any `x : (i : Î¹) â†’ X i`,
  -- as `Fâ‚™` technically depends on all the variables, but really depends only on the first `k + 1`.
  convert this using 1
  refine (hcte n).dependsOn_lmarginalPTraj _ (mf n) fun i hi â†¦ ?_
  simp only [update, updateFinset, mem_Iic, F]
  split_ifs with h1 h2 <;> try rfl
  rw [mem_coe, mem_Iic] at hi
  omega

/-- The indicator of a cylinder only depends on the variables whose the cylinder depends on. -/
theorem dependsOn_cylinder_indicator {Î¹ : Type*} {Î± : Î¹ â†’ Type*} {I : Finset Î¹}
    (S : Set ((i : I) â†’ Î± i)) :
    DependsOn ((cylinder I S).indicator (1 : ((Î  i, Î± i) â†’ â„â‰¥0âˆ))) I :=
  fun x y hxy â†¦ Set.indicator_const_eq_indicator_const (by simp [restrict_def, hxy])

/-- This is the key theorem to prove the existence of the `traj`:
the `trajContent` of a decreasing sequence of cylinders with empty intersection
converges to `0`.

This implies the `Ïƒ`-additivity of `trajContent` (see `sigma_additive_addContent_of_tendsto_zero`),
which allows to extend it to the `Ïƒ`-algebra by CarathÃ©odory's theorem. -/
theorem trajContent_tendsto_zero {A : â„• â†’ Set (Î  n, X n)}
    (A_mem : âˆ€ n, A n âˆˆ measurableCylinders X) (A_anti : Antitone A) (A_inter : â‹‚ n, A n = âˆ…)
    {p : â„•} (xâ‚€ : Î  i : Iic p, X i) :
    Tendsto (fun n â†¦ trajContent Îº xâ‚€ (A n)) atTop (ğ“ 0) := by
  have _ n : Nonempty (X n) := by
    induction n using Nat.case_strong_induction_on with
    | hz => exact âŸ¨xâ‚€ âŸ¨0, mem_Iic.2 (zero_le _)âŸ©âŸ©
    | hi m hm =>
      have : Nonempty (Î  i : Iic m, X i) :=
        âŸ¨fun i â†¦ @Classical.ofNonempty _ (hm i.1 (mem_Iic.1 i.2))âŸ©
      exact ProbabilityMeasure.nonempty âŸ¨Îº m Classical.ofNonempty, inferInstanceâŸ©
  -- `Aâ‚™` is a cylinder, it can be written as `cylinder (Iic (a n)) Sâ‚™`.
  have A_cyl n : âˆƒ a S, MeasurableSet S âˆ§ A n = cylinder (Iic a) S := by
    simpa [cylinders_nat] using A_mem n
  choose a S mS A_eq using A_cyl
  -- We write `Ï‡â‚™` for the indicator function of `Aâ‚™`.
  let Ï‡ n := (A n).indicator (1 : (Î  n, X n) â†’ â„â‰¥0âˆ)
  -- `Ï‡â‚™` is measurable.
  have mÏ‡ n : Measurable (Ï‡ n) := by
    simp_rw [Ï‡, A_eq]
    exact (measurable_indicator_const_iff 1).2 <| (mS n).cylinder
  -- `Ï‡â‚™` only depends on the first coordinates.
  have Ï‡_dep n : DependsOn (Ï‡ n) (Iic (a n)) := by
    simp_rw [Ï‡, A_eq]
    exact dependsOn_cylinder_indicator _
  -- Therefore its integral against `ptraj Îº k (a n)` is constant.
  have lma_const x y n :
      lmarginalPTraj Îº p (a n) (Ï‡ n) (updateFinset x _ xâ‚€) =
      lmarginalPTraj Îº p (a n) (Ï‡ n) (updateFinset y _ xâ‚€) := by
    refine (Ï‡_dep n).dependsOn_lmarginalPTraj p (mÏ‡ n) fun i hi â†¦ ?_
    rw [mem_coe, mem_Iic] at hi
    simp [updateFinset, hi]
  -- As `(Aâ‚™)` is non-increasing, so is `(Ï‡â‚™)`.
  have Ï‡_anti : Antitone Ï‡ := fun m n hmn y â†¦ by
    apply Set.indicator_le fun a ha â†¦ ?_
    simp [Ï‡, A_anti hmn ha]
  -- Integrating `Ï‡â‚™` further than the last coordinate it depends on does nothing.
  -- This is used to then show that the integral of `Ï‡â‚™` from time `k` is non-increasing.
  have lma_inv k M n (h : a n â‰¤ M) : lmarginalPTraj Îº k M (Ï‡ n) = lmarginalPTraj Îº k (a n) (Ï‡ n) :=
    (Ï‡_dep n).lmarginalPTraj_const_right (mÏ‡ n) h le_rfl
  -- the integral of `Ï‡â‚™` from time `k` is non-increasing.
  have anti_lma k x : Antitone fun n â†¦ lmarginalPTraj Îº k (a n) (Ï‡ n) x := by
    intro m n hmn
    simp only
    rw [â† lma_inv k ((a n).max (a m)) n (le_max_left _ _),
      â† lma_inv k ((a n).max (a m)) m (le_max_right _ _)]
    exact lmarginalPTraj_mono _ _ (Ï‡_anti hmn) _
  -- Therefore it converges to some function `lâ‚–`.
  have this k x : âˆƒ l, Tendsto (fun n â†¦ lmarginalPTraj Îº k (a n) (Ï‡ n) x) atTop (ğ“ l) := by
    obtain h | h := tendsto_of_antitone (anti_lma k x)
    Â· rw [OrderBot.atBot_eq] at h
      exact âŸ¨0, h.mono_right <| pure_le_nhds 0âŸ©
    Â· exact h
  choose l hl using this
  -- `lâ‚š` is constant because it is the limit of constant functions: we call it `Îµ`.
  have l_const x y : l p (updateFinset x _ xâ‚€) = l p (updateFinset y _ xâ‚€) := by
    have := hl p (updateFinset x _ xâ‚€)
    simp_rw [lma_const x y] at this
    exact tendsto_nhds_unique this (hl p _)
  obtain âŸ¨Îµ, hÎµâŸ© : âˆƒ Îµ, âˆ€ x, l p (updateFinset x _ xâ‚€) = Îµ :=
      âŸ¨l p (updateFinset Classical.ofNonempty _ xâ‚€), fun x â†¦ l_const _ _âŸ©
  -- As the sequence is decreasing, `Îµ â‰¤ âˆ« Ï‡â‚™`.
  have hpos x n : Îµ â‰¤ lmarginalPTraj Îº p (a n) (Ï‡ n) (updateFinset x _ xâ‚€) :=
    hÎµ x â–¸ ((anti_lma p _).le_of_tendsto (hl p _)) n
  -- Also, the indicators are bounded by `1`.
  have Ï‡_le n x : Ï‡ n x â‰¤ 1 := by
    apply Set.indicator_le
    simp
  -- We have all the conditions to apply `le_lmarginalPTraj_succ`.
  -- This allows us to recursively build a sequence `z` with the following property:
  -- for any `k â‰¥ p` and `n`, integrating `Ï‡ n` from time `k` to time `a n`
  -- with the trajectory up to `k` being equal to `z` gives something greater than `Îµ`.
  choose! ind hind using
    fun k y h â†¦ le_lmarginalPTraj_succ Îº Ï‡_dep mÏ‡ (by norm_num : (1 : â„â‰¥0âˆ) â‰  âˆ)
      Ï‡_le (anti_lma (k + 1)) (hl (k + 1)) Îµ y h
  let z := iterateInduction xâ‚€ ind
  have main k (hk : p â‰¤ k) : âˆ€ x n,
      Îµ â‰¤ lmarginalPTraj Îº k (a n) (Ï‡ n) (updateFinset x _ (frestrictLe k z)) := by
    induction k, hk using Nat.le_induction with
    | base => exact fun x n â†¦ by simpa [z, frestrictLe_iterateInduction] using hpos x n
    | succ k hn h =>
      intro x n
      convert hind k (fun i â†¦ z i.1) h x n
      ext i
      simp only [updateFinset, mem_Iic, frestrictLe_apply, dite_eq_ite, update, Ï‡, z]
      split_ifs with h1 h2 h3 h4 h5
      any_goals omega
      any_goals rfl
      cases h2
      rw [iterateInduction, dif_neg (by omega)]
  -- We now want to prove that the integral of `Ï‡â‚™`, which is equal to the `trajContent`
  -- of `Aâ‚™`, converges to `0`.
  have aux x n : trajContent Îº xâ‚€ (A n) = lmarginalPTraj Îº p (a n) (Ï‡ n) (updateFinset x _ xâ‚€) := by
    simp_rw [Ï‡, A_eq]
    nth_rw 1 [â† frestrictLe_updateFinset x xâ‚€]
    exact trajContent_eq_lmarginalPTraj _ (mS n) ..
  simp_rw [aux z]
  convert hl p _
  rw [hÎµ]
  -- Which means that we want to prove that `Îµ = 0`. But if `Îµ > 0`, then for any `n`,
  -- choosing `k > aâ‚™` we get `Îµ â‰¤ Ï‡â‚™(zâ‚€, ..., z_{aâ‚™})` and therefore `z âˆˆ Aâ‚™`.
  -- This contradicts the fact that `(Aâ‚™)` has an empty intersection.
  by_contra!
  have mem n : z âˆˆ A n := by
    have : 0 < Ï‡ n z := by
      rw [â† lmarginalPTraj_le Îº (le_max_right p (a n)) (mÏ‡ n),
        â† updateFinset_frestrictLe (a := a n) z]
      simpa using lt_of_lt_of_le this.symm.bot_lt (main _ (le_max_left _ _) z n)
    exact Set.mem_of_indicator_ne_zero (ne_of_lt this).symm
  exact (A_inter â–¸ Set.mem_iInter.2 mem).elim

/-- The `trajContent` is sigma-subadditive. -/
theorem isSigmaSubadditive_trajContent {a : â„•} (xâ‚€ : Î  i : Iic a, X i) :
    (trajContent Îº xâ‚€).IsSigmaSubadditive := by
  refine isSigmaSubadditive_of_addContent_iUnion_eq_tsum
    isSetRing_measurableCylinders (fun f hf hf_Union hf' â†¦ ?_)
  refine addContent_iUnion_eq_sum_of_tendsto_zero isSetRing_measurableCylinders
    (trajContent Îº xâ‚€) (fun _ _ â†¦ trajContent_ne_top) ?_ hf hf_Union hf'
  exact fun s hs anti_s inter_s â†¦ trajContent_tendsto_zero Îº hs anti_s inter_s xâ‚€

/-- This function is the kernel given by the Ionescu-Tulcea theorem. -/
noncomputable def trajFun (a : â„•) (xâ‚€ : Î  i : Iic a, X i) :
    Measure (Î  n, X n) :=
  (trajContent Îº xâ‚€).measure isSetSemiring_measurableCylinders generateFrom_measurableCylinders.ge
    (isSigmaSubadditive_trajContent Îº xâ‚€)

theorem isProbabilityMeasure_trajFun (a : â„•) (xâ‚€ : Î  i : Iic a, X i) :
    IsProbabilityMeasure (trajFun Îº a xâ‚€) where
  measure_univ := by
    rw [â† cylinder_univ (Iic 0), trajFun, AddContent.measure_eq,
      trajContent_cylinder _ _ .univ, measure_univ]
    Â· exact generateFrom_measurableCylinders.symm
    Â· exact cylinder_mem_measurableCylinders _ _ .univ

theorem isProjectiveLimit_trajFun (a : â„•) (xâ‚€ : Î  i : Iic a, X i) :
    IsProjectiveLimit (trajFun Îº a xâ‚€) (inducedFamily (fun n â†¦ ptraj Îº a n xâ‚€)) := by
  refine isProjectiveLimit_nat_iff (isProjectiveMeasureFamily_ptraj Îº xâ‚€) _ |>.2 fun n â†¦ ?_
  ext s ms
  rw [Measure.map_apply (measurable_frestrictLe n) ms, trajFun, AddContent.measure_eq, trajContent,
    projectiveFamilyContent_congr _ (frestrictLe n â»Â¹' s) rfl ms]
  Â· exact generateFrom_measurableCylinders.symm
  Â· exact cylinder_mem_measurableCylinders _ _ ms

theorem measurable_trajFun (a : â„•) : Measurable (trajFun Îº a) := by
  apply Measure.measurable_of_measurable_coe
  refine MeasurableSpace.induction_on_inter
    (C := fun t ht â†¦ Measurable (fun xâ‚€ â†¦ trajFun Îº a xâ‚€ t))
    (s := measurableCylinders X) generateFrom_measurableCylinders.symm
    isPiSystem_measurableCylinders (by simp) (fun t ht â†¦ ?cylinder) (fun t mt ht â†¦ ?compl)
    (fun f disf mf hf â†¦ ?union)
  Â· obtain âŸ¨N, S, mS, t_eqâŸ© : âˆƒ N S, MeasurableSet S âˆ§ t = cylinder (Iic N) S := by
      simpa [cylinders_nat] using ht
    simp_rw [trajFun, AddContent.measure_eq _ _ generateFrom_measurableCylinders.symm _ ht,
      trajContent, projectiveFamilyContent_congr _ t t_eq mS, inducedFamily]
    refine Measure.measurable_measure.1 ?_ _ mS
    exact (Measure.measurable_map _ (measurable_restrictâ‚‚ _)).comp (measurable _)
  Â· have := isProbabilityMeasure_trajFun Îº a
    simpa [measure_compl mt (measure_ne_top _ _)] using Measurable.const_sub ht _
  Â· simpa [measure_iUnion disf mf] using Measurable.ennreal_tsum hf

/-- *Ionescu-Tulcea Theorem* : Given a family of kernels `Îº n` taking variables in `Iic n` with
value in `X (n + 1)`, the kernel `traj Îº a` takes a variable `x` depending on the
variables `i â‰¤ a` and associates to it a kernel on trajectories depending on all variables,
where the entries with index `â‰¤ a` are those of `x`, and then one follows iteratively the
kernels `Îº a`, then `Îº (a + 1)`, and so on.

The fact that such a kernel exists on infinite trajectories is not obvious, and is the content of
the Ionescu-Tulcea theorem. We follow the proof of Theorem 8.24 in
[O. Kallenberg, *Foundations of Modern Probability*][kallenberg2021]. For a more detailed proof
in the case of constant kernels (i.e. measures),
see Proposition 10.6.1 in [D. L. Cohn, *Measure Theory*][cohn2013measure]. -/
noncomputable def traj (a : â„•) : Kernel (Î  i : Iic a, X i) (Î  n, X n) where
  toFun := trajFun Îº a
  measurable' := measurable_trajFun Îº a

end definition

section basic

lemma traj_apply (a : â„•) (x : Î  i : Iic a, X i) : traj Îº a x = trajFun Îº a x := rfl

instance (a : â„•) : IsMarkovKernel (traj Îº a) := âŸ¨fun _ â†¦ isProbabilityMeasure_trajFun ..âŸ©

lemma traj_map_frestrictLe (a b : â„•) : (traj Îº a).map (frestrictLe b) = ptraj Îº a b := by
  ext1 x
  rw [map_apply, traj_apply, frestrictLe, isProjectiveLimit_trajFun, inducedFamily_Iic]
  fun_prop

lemma traj_map_frestrictLe_apply (a b : â„•) (x : Î  i : Iic a, X i) :
    (traj Îº a x).map (frestrictLe b) = ptraj Îº a b x := by
  rw [â† map_apply _ (measurable_frestrictLe b), traj_map_frestrictLe]

lemma traj_map_frestrictLe_of_le {a b : â„•} (hab : a â‰¤ b) :
    (traj Îº b).map (frestrictLe a) =
      deterministic (frestrictLeâ‚‚ hab) (measurable_frestrictLeâ‚‚ _) := by
  rw [traj_map_frestrictLe, ptraj_le]

/-- To check that `Î· = traj Îº a` it is enough to show that the restriction of `Î·` to variables `â‰¤ b`
is `ptraj Îº a b` for any `b â‰¥ n`. -/
theorem eq_traj' {a : â„•} (n : â„•) (Î· : Kernel (Î  i : Iic a, X i) (Î  n, X n))
    (hÎ· : âˆ€ b â‰¥ n, Î·.map (frestrictLe b) = ptraj Îº a b) : Î· = traj Îº a := by
  ext1 x
  refine ((isProjectiveLimit_trajFun _ _ _).unique ?_).symm
  rw [isProjectiveLimit_nat_iff' _ _ n]
  Â· intro k hk
    rw [inducedFamily_Iic, â† map_apply _ (measurable_frestrictLe k), hÎ· k hk]
  Â· exact isProjectiveMeasureFamily_ptraj Îº x

/-- To check that `Î· = traj Îº a` it is enough to show that the restriction of `Î·` to variables `â‰¤ b`
is `ptraj Îº a b`. -/
theorem eq_traj {a : â„•} (Î· : Kernel (Î  i : Iic a, X i) (Î  n, X n))
    (hÎ· : âˆ€ b, Î·.map (frestrictLe b) = ptraj Îº a b) : Î· = traj Îº a :=
  eq_traj' Îº 0 Î· fun b _ â†¦ hÎ· b

/-- Given the distribution up to tome `a`, `ptraj Îº a b` gives the distribution of the trajectory
up to time `b`, and composing this with `traj Îº b` gives the distribution
of the whole trajectory. -/
theorem traj_comp_ptraj {a b : â„•} (hab : a â‰¤ b) : (traj Îº b) âˆ˜â‚– (ptraj Îº a b) = traj Îº a := by
  refine eq_traj _ _ fun n â†¦ ?_
  rw [map_comp, traj_map_frestrictLe, ptraj_comp_ptraj' _ hab]

def _root_.restrictf {Î¹ : Type*} {Î± : Î¹ â†’ Type*} {s : Finset Î¹} {t : Set Î¹} (hst : s.toSet âŠ† t)
    (a : Î  i : t, Î± i) (i : s) : Î± i := a âŸ¨i.1, hst i.2âŸ©

@[measurability, fun_prop]
lemma measurable_restrictf {Î¹ : Type*} {Î± : Î¹ â†’ Type*} [âˆ€ i, MeasurableSpace (Î± i)]
    {s : Finset Î¹} {t : Set Î¹} (hst : s.toSet âŠ† t) :
    Measurable (restrictf (Î± := Î±) hst) :=
  measurable_pi_lambda _ fun _ â†¦ measurable_pi_apply _

lemma restrictf_comp_restrict {Î¹ : Type*} {Î± : Î¹ â†’ Type*} {s : Finset Î¹} {t : Set Î¹}
    (hst : s.toSet âŠ† t) : (restrictf (Î± := Î±) hst) âˆ˜ t.restrict = s.restrict := rfl

/-- This theorem shows that `traj Îº n` is, up to an equivalence, the product of
a determinstic kernel with another kernel. This is an intermediate result to compute integrals
with respect to this kernel. -/
theorem traj_eq_prod (a : â„•) :
    traj Îº a = (Kernel.id Ã—â‚– (traj Îº a).map (Set.Ioi a).restrict).map (IicProdIoi a) := by
  refine (eq_traj' _ (a + 1) _ fun b hb â†¦ ?_).symm
  rw [â† map_comp_right]
  conv_lhs => enter [2]; change (IicProdIoc a b) âˆ˜
    (Prod.map id (restrictf (coe_Ioc a b â–¸ Set.Ioc_subset_Ioi_self)))
  Â· rw [map_comp_right, â† map_prod_map, â† map_comp_right, restrictf_comp_restrict,
      â† restrictâ‚‚_comp_restrict Ioc_subset_Iic_self, â† frestrictLe, map_comp_right,
      traj_map_frestrictLe, map_id, â† ptraj_eq_prod]
    all_goals fun_prop
  all_goals fun_prop

theorem traj_map_updateFinset {n : â„•} (x : Î  i : Iic n, X i) :
    (traj Îº n x).map (updateFinset Â· _ x) = traj Îº n x := by
  nth_rw 2 [traj_eq_prod]
  have : (updateFinset Â· _ x) = IicProdIoi n âˆ˜ (Prod.mk x) âˆ˜ (Set.Ioi n).restrict := by
    ext; simp [IicProdIoi, updateFinset]
  rw [this, â† Function.comp_assoc, â† Measure.map_map, â† Measure.map_map, map_apply, prod_apply,
    map_apply, id_apply, Measure.dirac_prod]
  all_goals fun_prop

end basic

section integral

/-! ### Integrals and `traj` -/

variable {E : Type*} [NormedAddCommGroup E]

theorem integrable_traj {a b : â„•} (hab : a â‰¤ b) {f : (Î  n, X n) â†’ E}
    (xâ‚€ : Î  i : Iic a, X i) (i_f : Integrable f (traj Îº a xâ‚€)) :
    âˆ€áµ x âˆ‚traj Îº a xâ‚€, Integrable f (traj Îº b (frestrictLe b x)) := by
  rw [â† traj_comp_ptraj _ hab, integrable_comp_iff] at i_f
  Â· apply ae_of_ae_map (p := fun x â†¦ Integrable f (traj Îº b x))
    Â· fun_prop
    Â· convert i_f.1
      rw [â† traj_map_frestrictLe, Kernel.map_apply _ (measurable_frestrictLe _)]
  Â· exact i_f.aestronglyMeasurable

theorem aestronglyMeasurable_traj {a b : â„•} (hab : a â‰¤ b) {f : (Î  n, X n) â†’ E}
    {xâ‚€ : Î  i : Iic a, X i} (hf : AEStronglyMeasurable f (traj Îº a xâ‚€)) :
    âˆ€áµ x âˆ‚ptraj Îº a b xâ‚€, AEStronglyMeasurable f (traj Îº b x) := by
  rw [â† traj_comp_ptraj Îº hab] at hf
  exact hf.comp

variable [NormedSpace â„ E]

variable {Îº} in
/-- When computing `âˆ« x, f x âˆ‚traj Îº n xâ‚€`, because the trajectory up to time `n` is
determined by `xâ‚€` we can replace `x` by `updateFinset x _ xâ‚€`. -/
theorem integral_traj {a : â„•} (xâ‚€ : Î  i : Iic a, X i) {f : (Î  n, X n) â†’ E}
    (mf : AEStronglyMeasurable f (traj Îº a xâ‚€)) :
    âˆ« x, f x âˆ‚traj Îº a xâ‚€ = âˆ« x, f (updateFinset x _ xâ‚€) âˆ‚traj Îº a xâ‚€ := by
  nth_rw 1 [â† traj_map_updateFinset, integral_map]
  Â· exact measurable_updateFinset_left.aemeasurable
  Â· convert mf
    rw [traj_map_updateFinset]

lemma ptraj_comp_ptrajProd_traj {a b : â„•} (hab : a â‰¤ b) (u : Î  i : Iic a, X i) :
    (traj Îº a u).map (fun x â†¦ (frestrictLe b x, x)) = (ptraj Îº a b u) âŠ—â‚˜ (traj Îº b) := by
  ext s ms
  rw [Measure.map_apply, Measure.compProd_apply, â† traj_comp_ptraj _ hab, comp_apply']
  Â· congr with x
    rw [â† traj_map_updateFinset, Measure.map_apply, Measure.map_apply]
    Â· congr with y
      simp only [Set.mem_preimage]
      congrm (fun i â†¦ ?_, fun i â†¦ ?_) âˆˆ s <;> simp [updateFinset]
    any_goals fun_prop
    all_goals exact ms.preimage (by fun_prop)
  any_goals exact ms.preimage (by fun_prop)
  fun_prop

variable {Îº}

theorem integral_traj_ptraj' {a b : â„•} (hab : a â‰¤ b) {xâ‚€ : Î  i : Iic a, X i}
    {f : (Î  i : Iic b, X i) â†’ (Î  n : â„•, X n) â†’ E}
    (hf : Integrable f.uncurry ((ptraj Îº a b xâ‚€) âŠ—â‚˜ (traj Îº b))) :
    âˆ« x, âˆ« y, f x y âˆ‚traj Îº b x âˆ‚ptraj Îº a b xâ‚€ = âˆ« x, f (frestrictLe b x) x âˆ‚traj Îº a xâ‚€ := by
  have hf1 := hf
  rw [â† ptraj_comp_ptrajProd_traj Îº hab] at hf1
  replace hf1 := hf1.comp_measurable (by fun_prop)
  have hf2 := aestronglyMeasurable_traj Îº hab hf1.1
  rw [â† traj_comp_ptraj Îº hab, Kernel.integral_comp]
  Â· apply integral_congr_ae
    filter_upwards [hf.1.compProd, hf2] with x h1 h2
    rw [integral_traj _ h1]
    nth_rw 2 [integral_traj]
    Â· simp_rw [frestrictLe_updateFinset]
    Â· exact h2
  Â· rwa [traj_comp_ptraj _ hab]

theorem integral_traj_ptraj {a b : â„•} (hab : a â‰¤ b) {xâ‚€ : Î  i : Iic a, X i}
    {f : (Î  n : â„•, X n) â†’ E} (hf : Integrable f (traj Îº a xâ‚€)) :
    âˆ« x, âˆ« y, f y âˆ‚traj Îº b x âˆ‚ptraj Îº a b xâ‚€ = âˆ« x, f x âˆ‚traj Îº a xâ‚€ := by
  apply integral_traj_ptraj' hab
  rw [â† traj_comp_ptraj Îº hab, comp_apply, â† Measure.snd_compProd] at hf
  exact hf.comp_measurable measurable_snd

theorem setIntegral_traj_ptraj' {a b : â„•} (hab : a â‰¤ b) {u : (Î  i : Iic a, X i)}
    {f : (Î  i : Iic b, X i) â†’ (Î  n : â„•, X n) â†’ E}
    (hf : Integrable f.uncurry ((ptraj Îº a b u) âŠ—â‚˜ (traj Îº b)))
    {A : Set (Î  i : Iic b, X i)} (hA : MeasurableSet A) :
    âˆ« x in A, âˆ« y, f x y âˆ‚traj Îº b x âˆ‚ptraj Îº a b u =
      âˆ« y in frestrictLe b â»Â¹' A, f (frestrictLe b y) y âˆ‚traj Îº a u := by
  rw [â† integral_integral_indicator _ hA, integral_traj_ptraj' hab]
  Â· simp_rw [â† Set.indicator_comp_right, â† integral_indicator (measurable_frestrictLe b hA)]
    rfl
  convert hf.indicator (hA.prod .univ)
  ext âŸ¨x, yâŸ©
  by_cases hx : x âˆˆ A <;> simp [uncurry_def, hx]

theorem setIntegral_traj_ptraj {a b : â„•} (hab : a â‰¤ b) {xâ‚€ : (Î  i : Iic a, X i)}
    {f : (Î  n : â„•, X n) â†’ E} (hf : Integrable f (traj Îº a xâ‚€))
    {A : Set (Î  i : Iic b, X i)} (hA : MeasurableSet A) :
    âˆ« x in A, âˆ« y, f y âˆ‚traj Îº b x âˆ‚ptraj Îº a b xâ‚€ =
      âˆ« y in frestrictLe b â»Â¹' A, f y âˆ‚traj Îº a xâ‚€ := by
  refine setIntegral_traj_ptraj' hab ?_ hA
  rw [â† traj_comp_ptraj Îº hab, comp_apply, â† Measure.snd_compProd] at hf
  exact hf.comp_measurable measurable_snd

variable [CompleteSpace E]

open Filtration

theorem condExp_traj {a b : â„•} (hab : a â‰¤ b) {xâ‚€ : Î  i : Iic a, X i}
    {f : (Î  n, X n) â†’ E} (i_f : Integrable f (traj Îº a xâ‚€)) :
    (traj Îº a xâ‚€)[f|piLE X b] =áµ[traj Îº a xâ‚€]
      fun x â†¦ âˆ« y, f y âˆ‚traj Îº b (frestrictLe b x) := by
  have i_f' : Integrable (fun x â†¦ âˆ« y, f y âˆ‚(traj Îº b) x)
      (((traj Îº a) xâ‚€).map (frestrictLe b)) := by
    rw [â† map_apply _ (measurable_frestrictLe _), traj_map_frestrictLe _ _]
    rw [â† traj_comp_ptraj _ hab] at i_f
    exact i_f.integral_comp

  refine ae_eq_condExp_of_forall_setIntegral_eq ((piLE X).le _) i_f
    (fun s _ _ â†¦ i_f'.comp_aemeasurable (measurable_frestrictLe b).aemeasurable |>.integrableOn)
    ?_ ?_ |>.symm <;> rw [piLE_eq_comap_frestrictLe]
  Â· rintro - âŸ¨t, mt, rflâŸ© -
    simp_rw [Function.comp_apply]
    rw [â† setIntegral_map mt i_f'.1, â† map_apply, traj_map_frestrictLe,
      setIntegral_traj_ptraj hab i_f mt]
    all_goals fun_prop
  Â· exact (i_f'.1.comp_ae_measurable' (measurable_frestrictLe b).aemeasurable)


variable (Îº)

theorem condExp_traj' {a b c : â„•} (hab : a â‰¤ b) (hbc : b â‰¤ c)
    (xâ‚€ : Î  i : Iic a, X i) (f : (Î  n, X n) â†’ E) :
    (traj Îº a xâ‚€)[f|piLE X b] =áµ[traj Îº a xâ‚€]
      fun x â†¦ âˆ« y, ((traj Îº a xâ‚€)[f|piLE X c]) (updateFinset x _ y)
        âˆ‚ptraj Îº b c (frestrictLe b x) := by
  have i_cf : Integrable ((traj Îº a xâ‚€)[f|piLE X c]) (traj Îº a xâ‚€) :=
    integrable_condExp
  have mcf : StronglyMeasurable ((traj Îº a xâ‚€)[f|piLE X c]) :=
    stronglyMeasurable_condExp.mono ((piLE X).le c)
  filter_upwards [(piLE X).condExp_condExp f hbc, condExp_traj hab i_cf] with x h1 h2
  rw [â† h1, h2, â† traj_map_frestrictLe, Kernel.map_apply, integral_map]
  Â· congr with y
    apply dependsOn_of_stronglyMeasurable stronglyMeasurable_condExp
    simp only [Set.mem_Iic, updateFinset, mem_Iic, frestrictLe_apply, dite_eq_ite]
    exact fun i hi â†¦ (if_pos hi).symm
  any_goals fun_prop
  Â· exact (mcf.comp_measurable measurable_updateFinset).aestronglyMeasurable

end integral

end ProbabilityTheory.Kernel
