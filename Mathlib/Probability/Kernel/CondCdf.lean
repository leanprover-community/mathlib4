/-
Copyright (c) 2023 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Logic.Encodable.Basic
import Mathlib.Data.Set.Lattice
import Mathlib.MeasureTheory.Measure.Stieltjes
import Mathlib.MeasureTheory.Decomposition.RadonNikodym
import Mathlib.MeasureTheory.Constructions.Prod.Basic

#align_import probability.kernel.cond_cdf from "leanprover-community/mathlib"@"3b88f4005dc2e28d42f974cc1ce838f0dafb39b8"

/-!
# Conditional cumulative distribution function

Given `Ï : measure (Î± Ã— â„)`, we define the conditional cumulative distribution function
(conditional cdf) of `Ï`. It is a function `cond_cdf Ï : Î± â†’ â„ â†’ â„` such that if `Ï` is a finite
measure, then for all `a : Î±` `cond_cdf Ï a` is monotone and right-continuous with limit 0 at -âˆ
and limit 1 at +âˆ, and such that for all `x : â„`, `a â†¦ cond_cdf Ï a x` is measurable. For all
`x : â„` and measurable set `s`, that function satisfies
`âˆ«â» a in s, ennreal.of_real (cond_cdf Ï a x) âˆ‚Ï.fst = Ï (s Ã—Ë¢ Iic x)`.

## Main definitions

* `probability_theory.cond_cdf Ï : Î± â†’ stieltjes_function`: the conditional cdf of
  `Ï : measure (Î± Ã— â„)`. A `stieltjes_function` is a function `â„ â†’ â„` which is monotone and
  right-continuous.

## Main statements

* `probability_theory.set_lintegral_cond_cdf`: for all `a : Î±` and `x : â„`, all measurable set `s`,
  `âˆ«â» a in s, ennreal.of_real (cond_cdf Ï a x) âˆ‚Ï.fst = Ï (s Ã—Ë¢ Iic x)`.

## References

The construction of the conditional cdf in this file follows the proof of Theorem 3.4 in
[O. Kallenberg, Foundations of modern probability][kallenberg2021].

## TODO

* The conditional cdf can be used to define the cdf of a real measure by using the
  conditional cdf of `(measure.dirac unit.star).prod Î¼ : measure (unit Ã— â„)`.

-/


open MeasureTheory Set Filter TopologicalSpace

open scoped NNReal ENNReal MeasureTheory Topology

section AuxLemmasToBeMoved

variable {Î± Î² Î¹ : Type*}

theorem Real.iUnion_Iic_rat : â‹ƒ r : â„š, Iic (r : â„) = univ := by
  ext1 x
  simp only [mem_iUnion, mem_Iic, mem_univ, iff_true_iff]
  obtain âŸ¨r, hrâŸ© := exists_rat_gt x
  exact âŸ¨r, hr.leâŸ©
#align real.Union_Iic_rat Real.iUnion_Iic_rat

theorem Real.iInter_Iic_rat : â‹‚ r : â„š, Iic (r : â„) = âˆ… := by
  ext1 x
  simp only [mem_iInter, mem_Iic, mem_empty_iff_false, iff_false_iff, not_forall, not_le]
  exact exists_rat_lt x
#align real.Inter_Iic_rat Real.iInter_Iic_rat

-- todo after the port: move to order/filter/at_top_bot
theorem atBot_le_nhds_bot {Î± : Type*} [TopologicalSpace Î±] [LinearOrder Î±] [OrderBot Î±]
    [OrderTopology Î±] : (atBot : Filter Î±) â‰¤ ğ“ âŠ¥ := by
  cases subsingleton_or_nontrivial Î±
  Â· simp only [nhds_discrete, le_pure_iff, mem_atBot_sets, mem_singleton_iff,
      eq_iff_true_of_subsingleton, imp_true_iff, exists_const]
  have h : atBot.HasBasis (fun _ : Î± => True) Iic := @atBot_basis Î± _ _
  have h_nhds : (ğ“ âŠ¥).HasBasis (fun a : Î± => âŠ¥ < a) fun a => Iio a := @nhds_bot_basis Î± _ _ _ _ _
  intro s
  rw [h.mem_iff, h_nhds.mem_iff]
  rintro âŸ¨a, ha_bot_lt, h_Iio_a_subset_sâŸ©
  refine' âŸ¨âŠ¥, trivial, _root_.trans _ h_Iio_a_subset_sâŸ©
  simpa only [Iic_bot, singleton_subset_iff, mem_Iio]
#align at_bot_le_nhds_bot atBot_le_nhds_bot

-- todo after the port: move to order/filter/at_top_bot
theorem atTop_le_nhds_top {Î± : Type*} [TopologicalSpace Î±] [LinearOrder Î±] [OrderTop Î±]
    [OrderTopology Î±] : (atTop : Filter Î±) â‰¤ ğ“ âŠ¤ :=
  @atBot_le_nhds_bot Î±áµ’áµˆ _ _ _ _
#align at_top_le_nhds_top atTop_le_nhds_top

-- todo: move to measure_theory/measurable_space
/-- Monotone convergence for an infimum over a directed family and indexed by a countable type -/
theorem lintegral_iInf_directed_of_measurable {mÎ± : MeasurableSpace Î±} [Countable Î²]
    {f : Î² â†’ Î± â†’ â„â‰¥0âˆ} {Î¼ : Measure Î±} (hÎ¼ : Î¼ â‰  0) (hf : âˆ€ b, Measurable (f b))
    (hf_int : âˆ€ b, âˆ«â» a, f b a âˆ‚Î¼ â‰  âˆ) (h_directed : Directed (Â· â‰¥ Â·) f) :
    âˆ«â» a, â¨… b, f b a âˆ‚Î¼ = â¨… b, âˆ«â» a, f b a âˆ‚Î¼ := by
  cases nonempty_encodable Î²
  cases isEmpty_or_nonempty Î²
  Â· -- Porting note: the next `simp only` doesn't do anything, so added a workaround below.
    -- simp only [WithTop.iInf_empty, lintegral_const]
    conv =>
      lhs
      congr
      Â· skip
      Â· ext x
        rw [WithTop.iInf_empty]
    rw [WithTop.iInf_empty, lintegral_const]
    rw [ENNReal.top_mul', if_neg]
    simp only [Measure.measure_univ_eq_zero, hÎ¼, not_false_iff]
  inhabit Î²
  have : âˆ€ a, â¨… b, f b a = â¨… n, f (h_directed.sequence f n) a := by
    refine' fun a =>
      le_antisymm (le_iInf fun n => iInf_le _ _)
        (le_iInf fun b => iInf_le_of_le (Encodable.encode b + 1) _)
    exact h_directed.sequence_le b a
  -- Porting note: used `âˆ˜` below to deal with its reduced reducibility
  calc
    âˆ«â» a, â¨… b, f b a âˆ‚Î¼
    _ = âˆ«â» a, â¨… n, (f âˆ˜ h_directed.sequence f) n a âˆ‚Î¼ := by simp only [this, Function.comp_apply]
    _ = â¨… n, âˆ«â» a, (f âˆ˜ h_directed.sequence f) n a âˆ‚Î¼ := by
      rw [lintegral_iInf ?_ h_directed.sequence_anti]
      Â· exact hf_int _
      Â· exact (fun n => hf _)
    _ = â¨… b, âˆ«â» a, f b a âˆ‚Î¼ := by
      refine' le_antisymm (le_iInf fun b => _) (le_iInf fun n => _)
      Â· exact iInf_le_of_le (Encodable.encode b + 1) (lintegral_mono <| h_directed.sequence_le b)
      Â· exact iInf_le (fun b => âˆ«â» a, f b a âˆ‚Î¼) _
#align lintegral_infi_directed_of_measurable lintegral_iInf_directed_of_measurable

end AuxLemmasToBeMoved

namespace MeasureTheory.Measure

variable {Î± Î² : Type*} {mÎ± : MeasurableSpace Î±} (Ï : Measure (Î± Ã— â„))

/-- Measure on `Î±` such that for a measurable set `s`, `Ï.Iic_snd r s = Ï (s Ã—Ë¢ Iic r)`. -/
noncomputable def IicSnd (r : â„) : Measure Î± :=
  (Ï.restrict (univ Ã—Ë¢ Iic r)).fst
#align measure_theory.measure.Iic_snd MeasureTheory.Measure.IicSnd

theorem IicSnd_apply (r : â„) {s : Set Î±} (hs : MeasurableSet s) :
    Ï.IicSnd r s = Ï (s Ã—Ë¢ Iic r) := by
  rw [IicSnd, fst_apply hs,
    restrict_apply' (MeasurableSet.univ.prod (measurableSet_Iic : MeasurableSet (Iic r))), â†
    prod_univ, prod_inter_prod, inter_univ, univ_inter]
#align measure_theory.measure.Iic_snd_apply MeasureTheory.Measure.IicSnd_apply

theorem IicSnd_univ (r : â„) : Ï.IicSnd r univ = Ï (univ Ã—Ë¢ Iic r) :=
  IicSnd_apply Ï r MeasurableSet.univ
#align measure_theory.measure.Iic_snd_univ MeasureTheory.Measure.IicSnd_univ

theorem IicSnd_mono {r r' : â„} (h_le : r â‰¤ r') : Ï.IicSnd r â‰¤ Ï.IicSnd r' := by
  intro s hs
  simp_rw [IicSnd_apply Ï _ hs]
  refine' measure_mono (prod_subset_prod_iff.mpr (Or.inl âŸ¨subset_rfl, Iic_subset_Iic.mpr _âŸ©))
  exact mod_cast h_le
#align measure_theory.measure.Iic_snd_mono MeasureTheory.Measure.IicSnd_mono

theorem IicSnd_le_fst (r : â„) : Ï.IicSnd r â‰¤ Ï.fst := by
  intro s hs
  simp_rw [fst_apply hs, IicSnd_apply Ï r hs]
  exact measure_mono (prod_subset_preimage_fst _ _)
#align measure_theory.measure.Iic_snd_le_fst MeasureTheory.Measure.IicSnd_le_fst

theorem IicSnd_ac_fst (r : â„) : Ï.IicSnd r â‰ª Ï.fst :=
  Measure.absolutelyContinuous_of_le (IicSnd_le_fst Ï r)
#align measure_theory.measure.Iic_snd_ac_fst MeasureTheory.Measure.IicSnd_ac_fst

theorem IsFiniteMeasure.IicSnd {Ï : Measure (Î± Ã— â„)} [IsFiniteMeasure Ï] (r : â„) :
    IsFiniteMeasure (Ï.IicSnd r) :=
  isFiniteMeasure_of_le _ (IicSnd_le_fst Ï _)
#align measure_theory.measure.is_finite_measure.Iic_snd MeasureTheory.Measure.IsFiniteMeasure.IicSnd

theorem iInf_IicSnd_gt (t : â„š) {s : Set Î±} (hs : MeasurableSet s) [IsFiniteMeasure Ï] :
    â¨… r : { r' : â„š // t < r' }, Ï.IicSnd r s = Ï.IicSnd t s := by
  simp_rw [Ï.IicSnd_apply _ hs]
  rw [â† measure_iInter_eq_iInf]
  Â· rw [â† prod_iInter]
    congr with x : 1
    simp only [mem_iInter, mem_Iic, Subtype.forall, Subtype.coe_mk]
    refine' âŸ¨fun h => _, fun h a hta => h.trans _âŸ©
    Â· refine' le_of_forall_lt_rat_imp_le fun q htq => h q _
      exact mod_cast htq
    Â· exact mod_cast hta.le
  Â· exact fun _ => hs.prod measurableSet_Iic
  Â· refine' Monotone.directed_ge fun r r' hrr' => prod_subset_prod_iff.mpr (Or.inl âŸ¨subset_rfl, _âŸ©)
    refine' Iic_subset_Iic.mpr _
    exact mod_cast hrr'
  Â· exact âŸ¨âŸ¨t + 1, lt_add_one _âŸ©, measure_ne_top Ï _âŸ©
#align measure_theory.measure.infi_Iic_snd_gt MeasureTheory.Measure.iInf_IicSnd_gt

theorem tendsto_IicSnd_atTop {s : Set Î±} (hs : MeasurableSet s) :
    Tendsto (fun r : â„š => Ï.IicSnd r s) atTop (ğ“ (Ï.fst s)) := by
  simp_rw [Ï.IicSnd_apply _ hs, fst_apply hs, â† prod_univ]
  rw [â† Real.iUnion_Iic_rat, prod_iUnion]
  refine' tendsto_measure_iUnion fun r q hr_le_q x => _
  simp only [mem_prod, mem_Iic, and_imp]
  refine' fun hxs hxr => âŸ¨hxs, hxr.trans _âŸ©
  exact mod_cast hr_le_q
#align measure_theory.measure.tendsto_Iic_snd_at_top MeasureTheory.Measure.tendsto_IicSnd_atTop

theorem tendsto_IicSnd_atBot [IsFiniteMeasure Ï] {s : Set Î±} (hs : MeasurableSet s) :
    Tendsto (fun r : â„š => Ï.IicSnd r s) atBot (ğ“ 0) := by
  simp_rw [Ï.IicSnd_apply _ hs]
  have h_empty : Ï (s Ã—Ë¢ âˆ…) = 0 := by simp only [prod_empty, measure_empty]
  rw [â† h_empty, â† Real.iInter_Iic_rat, prod_iInter]
  suffices h_neg :
    Tendsto (fun r : â„š => Ï (s Ã—Ë¢ Iic â†‘(-r))) atTop (ğ“ (Ï (â‹‚ r : â„š, s Ã—Ë¢ Iic â†‘(-r))))
  Â· have h_inter_eq : â‹‚ r : â„š, s Ã—Ë¢ Iic â†‘(-r) = â‹‚ r : â„š, s Ã—Ë¢ Iic (r : â„) := by
      ext1 x
      simp only [Rat.cast_eq_id, id.def, mem_iInter, mem_prod, mem_Iic]
      refine' âŸ¨fun h i => âŸ¨(h i).1, _âŸ©, fun h i => âŸ¨(h i).1, _âŸ©âŸ© <;> have h' := h (-i)
      Â· rw [neg_neg] at h'; exact h'.2
      Â· exact h'.2
    rw [h_inter_eq] at h_neg
    have h_fun_eq : (fun r : â„š => Ï (s Ã—Ë¢ Iic (r : â„))) = fun r : â„š => Ï (s Ã—Ë¢ Iic â†‘(- -r)) := by
      simp_rw [neg_neg]
    rw [h_fun_eq]
    exact h_neg.comp tendsto_neg_atBot_atTop
  refine' tendsto_measure_iInter (fun q => hs.prod measurableSet_Iic) _ âŸ¨0, measure_ne_top Ï _âŸ©
  refine' fun q r hqr => prod_subset_prod_iff.mpr (Or.inl âŸ¨subset_rfl, fun x hx => _âŸ©)
  simp only [Rat.cast_neg, mem_Iic] at hx âŠ¢
  refine' hx.trans (neg_le_neg _)
  exact mod_cast hqr
#align measure_theory.measure.tendsto_Iic_snd_at_bot MeasureTheory.Measure.tendsto_IicSnd_atBot

end MeasureTheory.Measure

open MeasureTheory

namespace ProbabilityTheory

variable {Î± Î² Î¹ : Type*} {mÎ± : MeasurableSpace Î±}

attribute [local instance] MeasureTheory.Measure.IsFiniteMeasure.IicSnd

/-! ### Auxiliary definitions

We build towards the definition of `probability_theory.cond_cdf`. We first define
`probability_theory.pre_cdf`, a function defined on `Î± Ã— â„š` with the properties of a cdf almost
everywhere. We then introduce `probability_theory.cond_cdf_rat`, a function on `Î± Ã— â„š` which has
the properties of a cdf for all `a : Î±`. We finally extend to `â„`. -/


/-- `pre_cdf` is the Radon-Nikodym derivative of `Ï.IicSnd` with respect to `Ï.fst` at each
`r : â„š`. This function `â„š â†’ Î± â†’ â„â‰¥0âˆ` is such that for almost all `a : Î±`, the function `â„š â†’ â„â‰¥0âˆ`
satisfies the properties of a cdf (monotone with limit 0 at -âˆ and 1 at +âˆ, right-continuous).

We define this function on `â„š` and not `â„` because `â„š` is countable, which allows us to prove
properties of the form `âˆ€áµ a âˆ‚Ï.fst, âˆ€ q, P (preCDF q a)`, instead of the weaker
`âˆ€ q, âˆ€áµ a âˆ‚Ï.fst, P (preCDF q a)`. -/
noncomputable def preCDF (Ï : Measure (Î± Ã— â„)) (r : â„š) : Î± â†’ â„â‰¥0âˆ :=
  Measure.rnDeriv (Ï.IicSnd r) Ï.fst
#align probability_theory.pre_cdf ProbabilityTheory.preCDF

theorem measurable_preCDF {Ï : Measure (Î± Ã— â„)} {r : â„š} : Measurable (preCDF Ï r) :=
  Measure.measurable_rnDeriv _ _
#align probability_theory.measurable_pre_cdf ProbabilityTheory.measurable_preCDF

theorem withDensity_preCDF (Ï : Measure (Î± Ã— â„)) (r : â„š) [IsFiniteMeasure Ï] :
    Ï.fst.withDensity (preCDF Ï r) = Ï.IicSnd r :=
  Measure.absolutelyContinuous_iff_withDensity_rnDeriv_eq.mp (Measure.IicSnd_ac_fst Ï r)
#align probability_theory.with_density_pre_cdf ProbabilityTheory.withDensity_preCDF

theorem set_lintegral_preCDF_fst (Ï : Measure (Î± Ã— â„)) (r : â„š) {s : Set Î±} (hs : MeasurableSet s)
    [IsFiniteMeasure Ï] : âˆ«â» x in s, preCDF Ï r x âˆ‚Ï.fst = Ï.IicSnd r s := by
  have : âˆ€ r, âˆ«â» x in s, preCDF Ï r x âˆ‚Ï.fst = âˆ«â» x in s, (preCDF Ï r * 1) x âˆ‚Ï.fst := by
    simp only [mul_one, eq_self_iff_true, forall_const]
  rw [this, â† set_lintegral_withDensity_eq_set_lintegral_mul _ measurable_preCDF _ hs]
  Â· simp only [withDensity_preCDF Ï r, Pi.one_apply, lintegral_one, Measure.restrict_apply,
      MeasurableSet.univ, univ_inter]
  Â· rw [(_ : (1 : Î± â†’ â„â‰¥0âˆ) = fun _ => 1)]
    exacts [measurable_const, rfl]
#align probability_theory.set_lintegral_pre_cdf_fst ProbabilityTheory.set_lintegral_preCDF_fst

theorem monotone_preCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] :
    âˆ€áµ a âˆ‚Ï.fst, Monotone fun r => preCDF Ï r a := by
  simp_rw [Monotone, ae_all_iff]
  refine' fun r r' hrr' =>
    ae_le_of_forall_set_lintegral_le_of_sigmaFinite measurable_preCDF measurable_preCDF
      fun s hs _ => _
  rw [set_lintegral_preCDF_fst Ï r hs, set_lintegral_preCDF_fst Ï r' hs]
  refine' Measure.IicSnd_mono Ï _ s hs
  exact mod_cast hrr'
#align probability_theory.monotone_pre_cdf ProbabilityTheory.monotone_preCDF

theorem set_lintegral_iInf_gt_preCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (t : â„š) {s : Set Î±}
    (hs : MeasurableSet s) : âˆ«â» x in s, â¨… r : Ioi t, preCDF Ï r x âˆ‚Ï.fst = Ï.IicSnd t s := by
  refine' le_antisymm _ _
  Â· have h : âˆ€ q : Ioi t, âˆ«â» x in s, â¨… r : Ioi t, preCDF Ï r x âˆ‚Ï.fst â‰¤ Ï.IicSnd q s := by
      intro q
      rw [â† set_lintegral_preCDF_fst Ï _ hs]
      refine' set_lintegral_mono_ae _ measurable_preCDF _
      Â· exact measurable_iInf fun _ => measurable_preCDF
      Â· filter_upwards [monotone_preCDF _] with a _
        exact fun _ => iInf_le _ q
    calc
      âˆ«â» x in s, â¨… r : Ioi t, preCDF Ï r x âˆ‚Ï.fst â‰¤ â¨… q : Ioi t, Ï.IicSnd q s := le_iInf h
      _ = Ï.IicSnd t s := Measure.iInf_IicSnd_gt Ï t hs
  Â· rw [(set_lintegral_preCDF_fst Ï t hs).symm]
    refine' set_lintegral_mono_ae measurable_preCDF _ _
    Â· exact measurable_iInf fun _ => measurable_preCDF
    Â· filter_upwards [monotone_preCDF _] with a ha_mono
      exact fun _ => le_iInf fun r => ha_mono (le_of_lt r.prop)
#align probability_theory.set_lintegral_infi_gt_pre_cdf ProbabilityTheory.set_lintegral_iInf_gt_preCDF

theorem preCDF_le_one (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] :
    âˆ€áµ a âˆ‚Ï.fst, âˆ€ r, preCDF Ï r a â‰¤ 1 := by
  rw [ae_all_iff]
  refine' fun r =>
    ae_le_of_forall_set_lintegral_le_of_sigmaFinite measurable_preCDF measurable_const
      fun s hs _ => _
  rw [set_lintegral_preCDF_fst Ï r hs]
  simp only [Pi.one_apply, lintegral_one, Measure.restrict_apply, MeasurableSet.univ, univ_inter]
  exact Measure.IicSnd_le_fst Ï r s hs
#align probability_theory.pre_cdf_le_one ProbabilityTheory.preCDF_le_one

theorem tendsto_lintegral_preCDF_atTop (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] :
    Tendsto (fun r => âˆ«â» a, preCDF Ï r a âˆ‚Ï.fst) atTop (ğ“ (Ï univ)) := by
  convert Ï.tendsto_IicSnd_atTop MeasurableSet.univ
  Â· rw [â† set_lintegral_univ, set_lintegral_preCDF_fst Ï _ MeasurableSet.univ]
  Â· exact Measure.fst_univ.symm
#align probability_theory.tendsto_lintegral_pre_cdf_at_top ProbabilityTheory.tendsto_lintegral_preCDF_atTop

theorem tendsto_lintegral_preCDF_atBot (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] :
    Tendsto (fun r => âˆ«â» a, preCDF Ï r a âˆ‚Ï.fst) atBot (ğ“ 0) := by
  convert Ï.tendsto_IicSnd_atBot MeasurableSet.univ
  rw [â† set_lintegral_univ, set_lintegral_preCDF_fst Ï _ MeasurableSet.univ]
#align probability_theory.tendsto_lintegral_pre_cdf_at_bot ProbabilityTheory.tendsto_lintegral_preCDF_atBot

theorem tendsto_preCDF_atTop_one (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] :
    âˆ€áµ a âˆ‚Ï.fst, Tendsto (fun r => preCDF Ï r a) atTop (ğ“ 1) := by
  -- We show first that `preCDF` has a limit almost everywhere. That limit has to be at most 1.
  -- We then show that the integral of `preCDF` tends to the integral of 1, and that it also tends
  -- to the integral of the limit. Since the limit is at most 1 and has same integral as 1, it is
  -- equal to 1 a.e.
  have h_mono := monotone_preCDF Ï
  have h_le_one := preCDF_le_one Ï
  -- `preCDF` has a limit a.e.
  have h_exists : âˆ€áµ a âˆ‚Ï.fst, âˆƒ l, Tendsto (fun r => preCDF Ï r a) atTop (ğ“ l) := by
    filter_upwards [h_mono, h_le_one] with a ha_mono ha_le_one
    have h_tendsto :
      Tendsto (fun r => preCDF Ï r a) atTop atTop âˆ¨
        âˆƒ l, Tendsto (fun r => preCDF Ï r a) atTop (ğ“ l) :=
      tendsto_of_monotone ha_mono
    cases' h_tendsto with h_absurd h_tendsto
    Â· rw [Monotone.tendsto_atTop_atTop_iff ha_mono] at h_absurd
      obtain âŸ¨r, hrâŸ© := h_absurd 2
      exact absurd (hr.trans (ha_le_one r)) ENNReal.one_lt_two.not_le
    Â· exact h_tendsto
  classical
  -- let `F` be the pointwise limit of `preCDF` where it exists, and 0 elsewhere.
  let F : Î± â†’ â„â‰¥0âˆ := fun a =>
    if h : âˆƒ l, Tendsto (fun r => preCDF Ï r a) atTop (ğ“ l) then h.choose else 0
  have h_tendsto_â„š : âˆ€áµ a âˆ‚Ï.fst, Tendsto (fun r => preCDF Ï r a) atTop (ğ“ (F a)) := by
    filter_upwards [h_exists] with a ha
    simp_rw [dif_pos ha]
    exact ha.choose_spec
  have h_tendsto_â„• : âˆ€áµ a âˆ‚Ï.fst, Tendsto (fun n : â„• => preCDF Ï n a) atTop (ğ“ (F a)) := by
    filter_upwards [h_tendsto_â„š] with a ha using ha.comp tendsto_nat_cast_atTop_atTop
  have hF_ae_meas : AEMeasurable F Ï.fst := by
    refine' aemeasurable_of_tendsto_metrizable_ae _ (fun n => _) h_tendsto_â„š
    exact measurable_preCDF.aemeasurable
  have hF_le_one : âˆ€áµ a âˆ‚Ï.fst, F a â‰¤ 1 := by
    filter_upwards [h_tendsto_â„š, h_le_one] with a ha ha_le using le_of_tendsto' ha ha_le
  -- it suffices to show that the limit `F` is 1 a.e.
  suffices âˆ€áµ a âˆ‚Ï.fst, F a = 1 by
    filter_upwards [h_tendsto_â„š, this] with a ha_tendsto ha_eq
    rwa [ha_eq] at ha_tendsto
  -- since `F` is at most 1, proving that its integral is the same as the integral of 1 will tell
  -- us that `F` is 1 a.e.
  have h_lintegral_eq : âˆ«â» a, F a âˆ‚Ï.fst = âˆ«â» _, 1 âˆ‚Ï.fst := by
    have h_lintegral :
      Tendsto (fun r : â„• => âˆ«â» a, preCDF Ï r a âˆ‚Ï.fst) atTop (ğ“ (âˆ«â» a, F a âˆ‚Ï.fst)) := by
      refine'
        lintegral_tendsto_of_tendsto_of_monotone
          (-- does this exist only for â„•?
          fun _ => measurable_preCDF.aemeasurable)
          _ h_tendsto_â„•
      filter_upwards [h_mono] with a ha
      refine' fun n m hnm => ha _
      exact mod_cast hnm
    have h_lintegral' :
      Tendsto (fun r : â„• => âˆ«â» a, preCDF Ï r a âˆ‚Ï.fst) atTop (ğ“ (âˆ«â» _, 1 âˆ‚Ï.fst)) := by
      rw [lintegral_one, Measure.fst_univ]
      exact (tendsto_lintegral_preCDF_atTop Ï).comp tendsto_nat_cast_atTop_atTop
    exact tendsto_nhds_unique h_lintegral h_lintegral'
  have : âˆ«â» a, 1 - F a âˆ‚Ï.fst = 0 := by
    rw [lintegral_sub' hF_ae_meas _ hF_le_one, h_lintegral_eq, tsub_self]
    calc
      âˆ«â» a, F a âˆ‚Ï.fst = âˆ«â» _, 1 âˆ‚Ï.fst := h_lintegral_eq
      _ = Ï.fst univ := lintegral_one
      _ = Ï univ := Measure.fst_univ
      _ â‰  âˆ := measure_ne_top Ï _
  rw [lintegral_eq_zero_iff' (aemeasurable_const.sub hF_ae_meas)] at this
  filter_upwards [this, hF_le_one] with ha h_one_sub_eq_zero h_le_one
  rw [Pi.zero_apply, tsub_eq_zero_iff_le] at h_one_sub_eq_zero
  exact le_antisymm h_le_one h_one_sub_eq_zero
#align probability_theory.tendsto_pre_cdf_at_top_one ProbabilityTheory.tendsto_preCDF_atTop_one

theorem tendsto_preCDF_atBot_zero (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] :
    âˆ€áµ a âˆ‚Ï.fst, Tendsto (fun r => preCDF Ï r a) atBot (ğ“ 0) := by
  -- We show first that `preCDF` has a limit in â„â‰¥0âˆ almost everywhere.
  -- We then show that the integral of `pre_cdf` tends to 0, and that it also tends
  -- to the integral of the limit. Since the limit has integral 0, it is equal to 0 a.e.
  suffices âˆ€áµ a âˆ‚Ï.fst, Tendsto (fun r => preCDF Ï (-r) a) atTop (ğ“ 0) by
    filter_upwards [this] with a ha
    have h_eq_neg : (fun r : â„š => preCDF Ï r a) = fun r : â„š => preCDF Ï (- -r) a := by
      simp_rw [neg_neg]
    rw [h_eq_neg]
    exact ha.comp tendsto_neg_atBot_atTop
  have h_exists : âˆ€áµ a âˆ‚Ï.fst, âˆƒ l, Tendsto (fun r => preCDF Ï (-r) a) atTop (ğ“ l) := by
    filter_upwards [monotone_preCDF Ï] with a ha
    have h_anti : Antitone fun r => preCDF Ï (-r) a := fun p q hpq => ha (neg_le_neg hpq)
    have h_tendsto :
      Tendsto (fun r => preCDF Ï (-r) a) atTop atBot âˆ¨
        âˆƒ l, Tendsto (fun r => preCDF Ï (-r) a) atTop (ğ“ l) :=
      tendsto_of_antitone h_anti
    cases' h_tendsto with h_bot h_tendsto
    Â· exact âŸ¨0, Tendsto.mono_right h_bot atBot_le_nhds_botâŸ©
    Â· exact h_tendsto
  classical
  let F : Î± â†’ â„â‰¥0âˆ := fun a =>
    if h : âˆƒ l, Tendsto (fun r => preCDF Ï (-r) a) atTop (ğ“ l) then h.choose else 0
  have h_tendsto : âˆ€áµ a âˆ‚Ï.fst, Tendsto (fun r => preCDF Ï (-r) a) atTop (ğ“ (F a)) := by
    filter_upwards [h_exists] with a ha
    simp_rw [dif_pos ha]
    exact ha.choose_spec
  suffices h_lintegral_eq : âˆ«â» a, F a âˆ‚Ï.fst = 0
  Â· have hF_ae_meas : AEMeasurable F Ï.fst := by
      refine' aemeasurable_of_tendsto_metrizable_ae _ (fun n => _) h_tendsto
      exact measurable_preCDF.aemeasurable
    rw [lintegral_eq_zero_iff' hF_ae_meas] at h_lintegral_eq
    filter_upwards [h_tendsto, h_lintegral_eq] with a ha_tendsto ha_eq
    rwa [ha_eq] at ha_tendsto
  have h_lintegral :
    Tendsto (fun r => âˆ«â» a, preCDF Ï (-r) a âˆ‚Ï.fst) atTop (ğ“ (âˆ«â» a, F a âˆ‚Ï.fst)) := by
    refine'
      tendsto_lintegral_filter_of_dominated_convergence (fun _ => 1)
        (eventually_of_forall fun _ => measurable_preCDF) (eventually_of_forall fun _ => _) _
        h_tendsto
    Â· filter_upwards [preCDF_le_one Ï] with a ha using ha _
    Â· rw [lintegral_one]
      exact measure_ne_top _ _
  have h_lintegral' : Tendsto (fun r => âˆ«â» a, preCDF Ï (-r) a âˆ‚Ï.fst) atTop (ğ“ 0) := by
    have h_lintegral_eq :
      (fun r => âˆ«â» a, preCDF Ï (-r) a âˆ‚Ï.fst) = fun r : â„š => Ï (univ Ã—Ë¢ Iic (-r : â„)) := by
      ext1 n
      rw [â† set_lintegral_univ, set_lintegral_preCDF_fst Ï _ MeasurableSet.univ,
        Measure.IicSnd_univ]
      norm_cast
    rw [h_lintegral_eq]
    have h_zero_eq_measure_iInter : (0 : â„â‰¥0âˆ) = Ï (â‹‚ r : â„š, univ Ã—Ë¢ Iic (-r : â„)) := by
      suffices â‹‚ r : â„š, Iic (-(r : â„)) = âˆ… by rw [â† prod_iInter, this, prod_empty, measure_empty]
      ext1 x
      simp only [mem_iInter, mem_Iic, mem_empty_iff_false, iff_false_iff, not_forall, not_le]
      simp_rw [neg_lt]
      exact exists_rat_gt _
    rw [h_zero_eq_measure_iInter]
    refine'
      tendsto_measure_iInter (fun n => MeasurableSet.univ.prod measurableSet_Iic)
        (fun i j hij x => _) âŸ¨0, measure_ne_top Ï _âŸ©
    simp only [mem_prod, mem_univ, mem_Iic, true_and_iff]
    refine' fun hxj => hxj.trans (neg_le_neg _)
    exact mod_cast hij
  exact tendsto_nhds_unique h_lintegral h_lintegral'
#align probability_theory.tendsto_pre_cdf_at_bot_zero ProbabilityTheory.tendsto_preCDF_atBot_zero

theorem inf_gt_preCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] :
    âˆ€áµ a âˆ‚Ï.fst, âˆ€ t : â„š, â¨… r : Ioi t, preCDF Ï r a = preCDF Ï t a := by
  rw [ae_all_iff]
  refine' fun t => ae_eq_of_forall_set_lintegral_eq_of_sigmaFinite _ measurable_preCDF _
  Â· exact measurable_iInf fun i => measurable_preCDF
  intro s hs _
  rw [set_lintegral_iInf_gt_preCDF Ï t hs, set_lintegral_preCDF_fst Ï t hs]
#align probability_theory.inf_gt_pre_cdf ProbabilityTheory.inf_gt_preCDF

section HasCondCDF

/-- A product measure on `Î± Ã— â„` is said to have a conditional cdf at `a : Î±` if `preCDF` is
monotone with limit 0 at -âˆ and 1 at +âˆ, and is right continuous.
This property holds almost everywhere (see `has_cond_cdf_ae`). -/
structure HasCondCDF (Ï : Measure (Î± Ã— â„)) (a : Î±) : Prop where
  mono : Monotone fun r => preCDF Ï r a
  le_one : âˆ€ r, preCDF Ï r a â‰¤ 1
  tendsto_atTop_one : Tendsto (fun r => preCDF Ï r a) atTop (ğ“ 1)
  tendsto_atBot_zero : Tendsto (fun r => preCDF Ï r a) atBot (ğ“ 0)
  iInf_rat_gt_eq : âˆ€ t : â„š, â¨… r : Ioi t, preCDF Ï r a = preCDF Ï t a
#align probability_theory.has_cond_cdf ProbabilityTheory.HasCondCDF

theorem hasCondCDF_ae (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] : âˆ€áµ a âˆ‚Ï.fst, HasCondCDF Ï a := by
  filter_upwards [monotone_preCDF Ï, preCDF_le_one Ï, tendsto_preCDF_atTop_one Ï,
    tendsto_preCDF_atBot_zero Ï, inf_gt_preCDF Ï] with a h1 h2 h3 h4 h5
  exact âŸ¨h1, h2, h3, h4, h5âŸ©
#align probability_theory.has_cond_cdf_ae ProbabilityTheory.hasCondCDF_ae

/-- A measurable set of elements of `Î±` such that `Ï` has a conditional cdf at all
`a âˆˆ condCDFSet`. -/
def condCDFSet (Ï : Measure (Î± Ã— â„)) : Set Î± :=
  (toMeasurable Ï.fst {b | Â¬HasCondCDF Ï b})á¶œ
#align probability_theory.cond_cdf_set ProbabilityTheory.condCDFSet

theorem measurableSet_condCDFSet (Ï : Measure (Î± Ã— â„)) : MeasurableSet (condCDFSet Ï) :=
  (measurableSet_toMeasurable _ _).compl
#align probability_theory.measurable_set_cond_cdf_set ProbabilityTheory.measurableSet_condCDFSet

theorem hasCondCDF_of_mem_condCDFSet {Ï : Measure (Î± Ã— â„)} {a : Î±} (h : a âˆˆ condCDFSet Ï) :
    HasCondCDF Ï a := by
  rw [condCDFSet, mem_compl_iff] at h
  have h_ss := subset_toMeasurable Ï.fst {b | Â¬HasCondCDF Ï b}
  by_contra ha
  exact h (h_ss ha)
#align probability_theory.has_cond_cdf_of_mem_cond_cdf_set ProbabilityTheory.hasCondCDF_of_mem_condCDFSet

theorem mem_condCDFSet_ae (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] :
    âˆ€áµ a âˆ‚Ï.fst, a âˆˆ condCDFSet Ï := by
  simp_rw [ae_iff, condCDFSet, not_mem_compl_iff, setOf_mem_eq, measure_toMeasurable]
  exact hasCondCDF_ae Ï
#align probability_theory.mem_cond_cdf_set_ae ProbabilityTheory.mem_condCDFSet_ae

end HasCondCDF

open scoped Classical

/-- Conditional cdf of the measure given the value on `Î±`, restricted to the rationals.
It is defined to be `pre_cdf` if `a âˆˆ condCDFSet`, and a default cdf-like function
otherwise. This is an auxiliary definition used to define `cond_cdf`. -/
noncomputable def condCDFRat (Ï : Measure (Î± Ã— â„)) : Î± â†’ â„š â†’ â„ := fun a =>
  if a âˆˆ condCDFSet Ï then fun r => (preCDF Ï r a).toReal else fun r => if r < 0 then 0 else 1
#align probability_theory.cond_cdf_rat ProbabilityTheory.condCDFRat

theorem condCDFRat_of_not_mem (Ï : Measure (Î± Ã— â„)) (a : Î±) (h : a âˆ‰ condCDFSet Ï) {r : â„š} :
    condCDFRat Ï a r = if r < 0 then 0 else 1 := by simp only [condCDFRat, h, if_false]
#align probability_theory.cond_cdf_rat_of_not_mem ProbabilityTheory.condCDFRat_of_not_mem

theorem condCDFRat_of_mem (Ï : Measure (Î± Ã— â„)) (a : Î±) (h : a âˆˆ condCDFSet Ï) (r : â„š) :
    condCDFRat Ï a r = (preCDF Ï r a).toReal := by simp only [condCDFRat, h, if_true]
#align probability_theory.cond_cdf_rat_of_mem ProbabilityTheory.condCDFRat_of_mem

theorem monotone_condCDFRat (Ï : Measure (Î± Ã— â„)) (a : Î±) : Monotone (condCDFRat Ï a) := by
  by_cases h : a âˆˆ condCDFSet Ï
  Â· simp only [condCDFRat, h, if_true, forall_const, and_self_iff]
    intro r r' hrr'
    have h' := hasCondCDF_of_mem_condCDFSet h
    have h_ne_top : âˆ€ r, preCDF Ï r a â‰  âˆ := fun r =>
      ((h'.le_one r).trans_lt ENNReal.one_lt_top).ne
    rw [ENNReal.toReal_le_toReal (h_ne_top _) (h_ne_top _)]
    exact h'.1 hrr'
  Â· simp only [condCDFRat, h, if_false]
    intro x y hxy
    dsimp only
    split_ifs with h_1 h_2 h_2
    exacts [le_rfl, zero_le_one, absurd (hxy.trans_lt h_2) h_1, le_rfl]
#align probability_theory.monotone_cond_cdf_rat ProbabilityTheory.monotone_condCDFRat

theorem measurable_condCDFRat (Ï : Measure (Î± Ã— â„)) (q : â„š) :
    Measurable fun a => condCDFRat Ï a q := by
  simp_rw [condCDFRat, ite_apply]
  exact
    Measurable.ite (measurableSet_condCDFSet Ï) measurable_preCDF.ennreal_toReal
      measurable_const
#align probability_theory.measurable_cond_cdf_rat ProbabilityTheory.measurable_condCDFRat

theorem condCDFRat_nonneg (Ï : Measure (Î± Ã— â„)) (a : Î±) (r : â„š) : 0 â‰¤ condCDFRat Ï a r := by
  -- Porting note: was
  -- unfold condCDFRat; split_ifs; exacts [ENNReal.toReal_nonneg, le_rfl, zero_le_one]
  unfold condCDFRat; split_ifs
  Â· exact ENNReal.toReal_nonneg
  dsimp only
  split_ifs
  exacts [le_rfl, zero_le_one]
#align probability_theory.cond_cdf_rat_nonneg ProbabilityTheory.condCDFRat_nonneg

theorem condCDFRat_le_one (Ï : Measure (Î± Ã— â„)) (a : Î±) (r : â„š) : condCDFRat Ï a r â‰¤ 1 := by
  unfold condCDFRat
  split_ifs with h
  Â· refine' ENNReal.toReal_le_of_le_ofReal zero_le_one _
    rw [ENNReal.ofReal_one]
    exact (hasCondCDF_of_mem_condCDFSet h).le_one r
  -- Porting note: added
  dsimp only; split_ifs
  exacts [zero_le_one, le_rfl]
#align probability_theory.cond_cdf_rat_le_one ProbabilityTheory.condCDFRat_le_one

theorem tendsto_condCDFRat_atBot (Ï : Measure (Î± Ã— â„)) (a : Î±) :
    Tendsto (condCDFRat Ï a) atBot (ğ“ 0) := by
  unfold condCDFRat
  split_ifs with h
  Â· rw [â† ENNReal.zero_toReal, ENNReal.tendsto_toReal_iff]
    Â· exact (hasCondCDF_of_mem_condCDFSet h).tendsto_atBot_zero
    Â· have h' := hasCondCDF_of_mem_condCDFSet h
      exact fun r => ((h'.le_one r).trans_lt ENNReal.one_lt_top).ne
    Â· exact ENNReal.zero_ne_top
  Â· refine' (tendsto_congr' _).mp tendsto_const_nhds
    rw [EventuallyEq, eventually_atBot]
    refine' âŸ¨-1, fun q hq => (if_pos (hq.trans_lt _)).symmâŸ©
    linarith
#align probability_theory.tendsto_cond_cdf_rat_at_bot ProbabilityTheory.tendsto_condCDFRat_atBot

theorem tendsto_condCDFRat_atTop (Ï : Measure (Î± Ã— â„)) (a : Î±) :
    Tendsto (condCDFRat Ï a) atTop (ğ“ 1) := by
  unfold condCDFRat
  split_ifs with h
  Â· have h' := hasCondCDF_of_mem_condCDFSet h
    rw [â† ENNReal.one_toReal, ENNReal.tendsto_toReal_iff]
    Â· exact h'.tendsto_atTop_one
    Â· exact fun r => ((h'.le_one r).trans_lt ENNReal.one_lt_top).ne
    Â· exact ENNReal.one_ne_top
  Â· refine' (tendsto_congr' _).mp tendsto_const_nhds
    rw [EventuallyEq, eventually_atTop]
    exact âŸ¨0, fun q hq => (if_neg (not_lt.mpr hq)).symmâŸ©
#align probability_theory.tendsto_cond_cdf_rat_at_top ProbabilityTheory.tendsto_condCDFRat_atTop

theorem condCDFRat_ae_eq (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (r : â„š) :
    (fun a => condCDFRat Ï a r) =áµ[Ï.fst] fun a => (preCDF Ï r a).toReal := by
  filter_upwards [mem_condCDFSet_ae Ï] with a ha using condCDFRat_of_mem Ï a ha r
#align probability_theory.cond_cdf_rat_ae_eq ProbabilityTheory.condCDFRat_ae_eq

theorem ofReal_condCDFRat_ae_eq (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (r : â„š) :
    (fun a => ENNReal.ofReal (condCDFRat Ï a r)) =áµ[Ï.fst] preCDF Ï r := by
  filter_upwards [condCDFRat_ae_eq Ï r, preCDF_le_one Ï] with a ha ha_le_one
  rw [ha, ENNReal.ofReal_toReal]
  exact ((ha_le_one r).trans_lt ENNReal.one_lt_top).ne
#align probability_theory.of_real_cond_cdf_rat_ae_eq ProbabilityTheory.ofReal_condCDFRat_ae_eq

theorem inf_gt_condCDFRat (Ï : Measure (Î± Ã— â„)) (a : Î±) (t : â„š) :
    â¨… r : Ioi t, condCDFRat Ï a r = condCDFRat Ï a t := by
  by_cases ha : a âˆˆ condCDFSet Ï
  Â· simp_rw [condCDFRat_of_mem Ï a ha]
    have ha' := hasCondCDF_of_mem_condCDFSet ha
    rw [â† ENNReal.toReal_iInf]
    Â· suffices â¨… i : â†¥(Ioi t), preCDF Ï (â†‘i) a = preCDF Ï t a by rw [this]
      rw [â† ha'.iInf_rat_gt_eq]
    Â· exact fun r => ((ha'.le_one r).trans_lt ENNReal.one_lt_top).ne
  Â· simp_rw [condCDFRat_of_not_mem Ï a ha]
    have h_bdd : BddBelow (range fun r : â†¥(Ioi t) => ite ((r : â„š) < 0) (0 : â„) 1) := by
      refine' âŸ¨0, fun x hx => _âŸ©
      obtain âŸ¨y, rflâŸ© := mem_range.mpr hx
      dsimp only
      split_ifs
      exacts [le_rfl, zero_le_one]
    split_ifs with h
    Â· refine' le_antisymm _ (le_ciInf fun x => _)
      Â· obtain âŸ¨q, htq, hq_negâŸ© : âˆƒ q, t < q âˆ§ q < 0 := by
          refine' âŸ¨t / 2, _, _âŸ©
          Â· linarith
          Â· linarith
        refine' (ciInf_le h_bdd âŸ¨q, htqâŸ©).trans _
        rw [if_pos]
        rwa [Subtype.coe_mk]
      Â· split_ifs
        exacts [le_rfl, zero_le_one]
    Â· refine' le_antisymm _ _
      Â· refine' (ciInf_le h_bdd âŸ¨t + 1, lt_add_one tâŸ©).trans _
        split_ifs
        exacts [zero_le_one, le_rfl]
      Â· refine' le_ciInf fun x => _
        rw [if_neg]
        rw [not_lt] at h âŠ¢
        exact h.trans (mem_Ioi.mp x.prop).le
#align probability_theory.inf_gt_cond_cdf_rat ProbabilityTheory.inf_gt_condCDFRat

/-- Conditional cdf of the measure given the value on `Î±`, as a plain function. This is an auxiliary
definition used to define `cond_cdf`. -/
noncomputable irreducible_def condCDF' (Ï : Measure (Î± Ã— â„)) : Î± â†’ â„ â†’ â„ := fun a t =>
  â¨… r : { r' : â„š // t < r' }, condCDFRat Ï a r
#align probability_theory.cond_cdf' ProbabilityTheory.condCDF'

theorem condCDF'_def' {Ï : Measure (Î± Ã— â„)} {a : Î±} {x : â„} :
    condCDF' Ï a x = â¨… r : { r : â„š // x < r }, condCDFRat Ï a r := by rw [condCDF']
#align probability_theory.cond_cdf'_def ProbabilityTheory.condCDF'_def'

theorem condCDF'_eq_condCDFRat (Ï : Measure (Î± Ã— â„)) (a : Î±) (r : â„š) :
    condCDF' Ï a r = condCDFRat Ï a r := by
  rw [â† inf_gt_condCDFRat Ï a r, condCDF']
  refine' Equiv.iInf_congr _ _
  Â· exact
      { toFun := fun t => âŸ¨t.1, mod_cast t.2âŸ©
        invFun := fun t => âŸ¨t.1, mod_cast t.2âŸ©
        left_inv := fun t => by simp only [Subtype.coe_eta]
        right_inv := fun t => by simp only [Subtype.coe_eta] }
  Â· intro t
    simp only [Equiv.coe_fn_mk, Subtype.coe_mk]
#align probability_theory.cond_cdf'_eq_cond_cdf_rat ProbabilityTheory.condCDF'_eq_condCDFRat

theorem condCDF'_nonneg (Ï : Measure (Î± Ã— â„)) (a : Î±) (r : â„) : 0 â‰¤ condCDF' Ï a r := by
  have : Nonempty { r' : â„š // r < â†‘r' } := by
    obtain âŸ¨r, hrxâŸ© := exists_rat_gt r
    exact âŸ¨âŸ¨r, hrxâŸ©âŸ©
  rw [condCDF'_def]
  exact le_ciInf fun r' => condCDFRat_nonneg Ï a _
#align probability_theory.cond_cdf'_nonneg ProbabilityTheory.condCDF'_nonneg

theorem bddBelow_range_condCDFRat_gt (Ï : Measure (Î± Ã— â„)) (a : Î±) (x : â„) :
    BddBelow (range fun r : { r' : â„š // x < â†‘r' } => condCDFRat Ï a r) := by
  refine' âŸ¨0, fun z => _âŸ©; rintro âŸ¨u, rflâŸ©; exact condCDFRat_nonneg Ï a _
#align probability_theory.bdd_below_range_cond_cdf_rat_gt ProbabilityTheory.bddBelow_range_condCDFRat_gt

theorem monotone_condCDF' (Ï : Measure (Î± Ã— â„)) (a : Î±) : Monotone (condCDF' Ï a) := by
  intro x y hxy
  have : Nonempty { r' : â„š // y < â†‘r' } := by
    obtain âŸ¨r, hrxâŸ© := exists_rat_gt y
    exact âŸ¨âŸ¨r, hrxâŸ©âŸ©
  simp_rw [condCDF'_def]
  refine' le_ciInf fun r => (ciInf_le _ _).trans_eq _
  Â· exact bddBelow_range_condCDFRat_gt Ï a x
  Â· exact âŸ¨r.1, hxy.trans_lt r.propâŸ©
  Â· rfl
#align probability_theory.monotone_cond_cdf' ProbabilityTheory.monotone_condCDF'

theorem continuousWithinAt_condCDF'_Ici (Ï : Measure (Î± Ã— â„)) (a : Î±) (x : â„) :
    ContinuousWithinAt (condCDF' Ï a) (Ici x) x := by
  rw [â† continuousWithinAt_Ioi_iff_Ici]
  convert Monotone.tendsto_nhdsWithin_Ioi (monotone_condCDF' Ï a) x
  rw [sInf_image']
  have h' : â¨… r : Ioi x, condCDF' Ï a r = â¨… r : { r' : â„š // x < r' }, condCDF' Ï a r := by
    refine' Real.iInf_Ioi_eq_iInf_rat_gt x _ (monotone_condCDF' Ï a)
    refine' âŸ¨0, fun z => _âŸ©
    rintro âŸ¨u, -, rflâŸ©
    exact condCDF'_nonneg Ï a u
  have h'' :
    â¨… r : { r' : â„š // x < r' }, condCDF' Ï a r =
      â¨… r : { r' : â„š // x < r' }, condCDFRat Ï a r := by
    congr with r
    exact condCDF'_eq_condCDFRat Ï a r
  rw [h', h'', ContinuousWithinAt]
  congr!
  exact condCDF'_def'
#align probability_theory.continuous_within_at_cond_cdf'_Ici ProbabilityTheory.continuousWithinAt_condCDF'_Ici

/-! ### Conditional cdf -/


/-- Conditional cdf of the measure given the value on `Î±`, as a Stieltjes function. -/
noncomputable def condCDF (Ï : Measure (Î± Ã— â„)) (a : Î±) : StieltjesFunction where
  toFun := condCDF' Ï a
  mono' := monotone_condCDF' Ï a
  right_continuous' x := continuousWithinAt_condCDF'_Ici Ï a x
#align probability_theory.cond_cdf ProbabilityTheory.condCDF

theorem condCDF_eq_condCDFRat (Ï : Measure (Î± Ã— â„)) (a : Î±) (r : â„š) :
    condCDF Ï a r = condCDFRat Ï a r :=
  condCDF'_eq_condCDFRat Ï a r
#align probability_theory.cond_cdf_eq_cond_cdf_rat ProbabilityTheory.condCDF_eq_condCDFRat

/-- The conditional cdf is non-negative for all `a : Î±`. -/
theorem condCDF_nonneg (Ï : Measure (Î± Ã— â„)) (a : Î±) (r : â„) : 0 â‰¤ condCDF Ï a r :=
  condCDF'_nonneg Ï a r
#align probability_theory.cond_cdf_nonneg ProbabilityTheory.condCDF_nonneg

/-- The conditional cdf is lower or equal to 1 for all `a : Î±`. -/
theorem condCDF_le_one (Ï : Measure (Î± Ã— â„)) (a : Î±) (x : â„) : condCDF Ï a x â‰¤ 1 := by
  obtain âŸ¨r, hrxâŸ© := exists_rat_gt x
  rw [â† StieltjesFunction.iInf_rat_gt_eq]
  simp_rw [condCDF_eq_condCDFRat]
  refine' ciInf_le_of_le (bddBelow_range_condCDFRat_gt Ï a x) _ (condCDFRat_le_one _ _ _)
  exact âŸ¨r, hrxâŸ©
#align probability_theory.cond_cdf_le_one ProbabilityTheory.condCDF_le_one

/-- The conditional cdf tends to 0 at -âˆ for all `a : Î±`. -/
theorem tendsto_condCDF_atBot (Ï : Measure (Î± Ã— â„)) (a : Î±) :
    Tendsto (condCDF Ï a) atBot (ğ“ 0) := by
  have h_exists : âˆ€ x : â„, âˆƒ q : â„š, x < q âˆ§ â†‘q < x + 1 := fun x => exists_rat_btwn (lt_add_one x)
  let qs : â„ â†’ â„š := fun x => (h_exists x).choose
  have hqs_tendsto : Tendsto qs atBot atBot := by
    rw [tendsto_atBot_atBot]
    refine' fun q => âŸ¨q - 1, fun y hy => _âŸ©
    have h_le : â†‘(qs y) â‰¤ (q : â„) - 1 + 1 :=
      (h_exists y).choose_spec.2.le.trans (add_le_add hy le_rfl)
    rw [sub_add_cancel] at h_le
    exact mod_cast h_le
  refine'
    tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds
      ((tendsto_condCDFRat_atBot Ï a).comp hqs_tendsto) (condCDF_nonneg Ï a) fun x => _
  rw [Function.comp_apply, â† condCDF_eq_condCDFRat]
  exact (condCDF Ï a).mono (h_exists x).choose_spec.1.le
#align probability_theory.tendsto_cond_cdf_at_bot ProbabilityTheory.tendsto_condCDF_atBot

/-- The conditional cdf tends to 1 at +âˆ for all `a : Î±`. -/
theorem tendsto_condCDF_atTop (Ï : Measure (Î± Ã— â„)) (a : Î±) :
    Tendsto (condCDF Ï a) atTop (ğ“ 1) := by
  have h_exists : âˆ€ x : â„, âˆƒ q : â„š, x - 1 < q âˆ§ â†‘q < x := fun x => exists_rat_btwn (sub_one_lt x)
  let qs : â„ â†’ â„š := fun x => (h_exists x).choose
  have hqs_tendsto : Tendsto qs atTop atTop := by
    rw [tendsto_atTop_atTop]
    refine' fun q => âŸ¨q + 1, fun y hy => _âŸ©
    have h_le : y - 1 â‰¤ qs y := (h_exists y).choose_spec.1.le
    rw [sub_le_iff_le_add] at h_le
    exact_mod_cast le_of_add_le_add_right (hy.trans h_le)
  refine'
    tendsto_of_tendsto_of_tendsto_of_le_of_le ((tendsto_condCDFRat_atTop Ï a).comp hqs_tendsto)
      tendsto_const_nhds _ (condCDF_le_one Ï a)
  intro x
  rw [Function.comp_apply, â† condCDF_eq_condCDFRat]
  exact (condCDF Ï a).mono (le_of_lt (h_exists x).choose_spec.2)
#align probability_theory.tendsto_cond_cdf_at_top ProbabilityTheory.tendsto_condCDF_atTop

theorem condCDF_ae_eq (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (r : â„š) :
    (fun a => condCDF Ï a r) =áµ[Ï.fst] fun a => (preCDF Ï r a).toReal := by
  filter_upwards [mem_condCDFSet_ae Ï] with a ha using
    (condCDF_eq_condCDFRat Ï a r).trans (condCDFRat_of_mem Ï a ha r)
#align probability_theory.cond_cdf_ae_eq ProbabilityTheory.condCDF_ae_eq

theorem ofReal_condCDF_ae_eq (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (r : â„š) :
    (fun a => ENNReal.ofReal (condCDF Ï a r)) =áµ[Ï.fst] preCDF Ï r := by
  filter_upwards [condCDF_ae_eq Ï r, preCDF_le_one Ï] with a ha ha_le_one
  rw [ha, ENNReal.ofReal_toReal]
  exact ((ha_le_one r).trans_lt ENNReal.one_lt_top).ne
#align probability_theory.of_real_cond_cdf_ae_eq ProbabilityTheory.ofReal_condCDF_ae_eq

/-- The conditional cdf is a measurable function of `a : Î±` for all `x : â„`. -/
theorem measurable_condCDF (Ï : Measure (Î± Ã— â„)) (x : â„) : Measurable fun a => condCDF Ï a x := by
  have : (fun a => condCDF Ï a x) = fun a => â¨… r : { r' : â„š // x < r' }, condCDFRat Ï a â†‘r := by
    ext1 a
    rw [â† StieltjesFunction.iInf_rat_gt_eq]
    congr with q
    rw [condCDF_eq_condCDFRat]
  rw [this]
  exact measurable_iInf (fun q => measurable_condCDFRat Ï q)
#align probability_theory.measurable_cond_cdf ProbabilityTheory.measurable_condCDF

/-- Auxiliary lemma for `set_lintegral_cond_cdf`. -/
theorem set_lintegral_condCDF_rat (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (r : â„š) {s : Set Î±}
    (hs : MeasurableSet s) :
    âˆ«â» a in s, ENNReal.ofReal (condCDF Ï a r) âˆ‚Ï.fst = Ï (s Ã—Ë¢ Iic (r : â„)) := by
  have : âˆ€áµ a âˆ‚Ï.fst, a âˆˆ s â†’ ENNReal.ofReal (condCDF Ï a r) = preCDF Ï r a := by
    filter_upwards [ofReal_condCDF_ae_eq Ï r] with a ha using fun _ => ha
  rw [set_lintegral_congr_fun hs this, set_lintegral_preCDF_fst Ï r hs]
  exact Ï.IicSnd_apply r hs
#align probability_theory.set_lintegral_cond_cdf_rat ProbabilityTheory.set_lintegral_condCDF_rat

theorem set_lintegral_condCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (x : â„) {s : Set Î±}
    (hs : MeasurableSet s) : âˆ«â» a in s, ENNReal.ofReal (condCDF Ï a x) âˆ‚Ï.fst = Ï (s Ã—Ë¢ Iic x) := by
  -- We have the result for `x : â„š` thanks to `set_lintegral_condCDF_rat`. We use the equality
  -- `condCDF Ï a x = â¨… r : {r' : â„š // x < r'}, condCDF Ï a r` and a monotone convergence
  -- argument to extend it to the reals.
  by_cases hÏ_zero : Ï.fst.restrict s = 0
  Â· rw [hÏ_zero, lintegral_zero_measure]
    refine' le_antisymm (zero_le _) _
    calc
      Ï (s Ã—Ë¢ Iic x) â‰¤ Ï (Prod.fst â»Â¹' s) := measure_mono (prod_subset_preimage_fst s (Iic x))
      _ = Ï.fst s := by rw [Measure.fst_apply hs]
      _ = Ï.fst.restrict s univ := by rw [Measure.restrict_apply_univ]
      _ = 0 := by simp only [hÏ_zero, Measure.coe_zero, Pi.zero_apply]
  have h :
    âˆ«â» a in s, ENNReal.ofReal (condCDF Ï a x) âˆ‚Ï.fst =
      âˆ«â» a in s, ENNReal.ofReal (â¨… r : { r' : â„š // x < r' }, condCDF Ï a r) âˆ‚Ï.fst := by
    congr with a : 1
    rw [â† (condCDF Ï a).iInf_rat_gt_eq x]
  have h_nonempty : Nonempty { r' : â„š // x < â†‘r' } := by
    obtain âŸ¨r, hrxâŸ© := exists_rat_gt x
    exact âŸ¨âŸ¨r, hrxâŸ©âŸ©
  rw [h]
  simp_rw [ENNReal.ofReal_cinfi]
  have h_coe : âˆ€ b : { r' : â„š // x < â†‘r' }, (b : â„) = ((b : â„š) : â„) := fun _ => by congr
  rw [lintegral_iInf_directed_of_measurable hÏ_zero fun q : { r' : â„š // x < â†‘r' } =>
      (measurable_condCDF Ï q).ennreal_ofReal]
  rotate_left
  Â· intro b
    rw [set_lintegral_condCDF_rat Ï _ hs]
    exact measure_ne_top Ï _
  Â· refine' Monotone.directed_ge fun i j hij a => ENNReal.ofReal_le_ofReal ((condCDF Ï a).mono _)
    rw [h_coe, h_coe]
    exact mod_cast hij
  simp_rw [set_lintegral_condCDF_rat Ï _ hs]
  rw [â† measure_iInter_eq_iInf]
  Â· rw [â† prod_iInter]
    congr with y
    simp only [mem_iInter, mem_Iic, Subtype.forall, Subtype.coe_mk]
    exact âŸ¨le_of_forall_lt_rat_imp_le, fun hyx q hq => hyx.trans hq.leâŸ©
  Â· exact fun i => hs.prod measurableSet_Iic
  Â· refine' Monotone.directed_ge fun i j hij => _
    refine' prod_subset_prod_iff.mpr (Or.inl âŸ¨subset_rfl, Iic_subset_Iic.mpr _âŸ©)
    exact mod_cast hij
  Â· exact âŸ¨h_nonempty.some, measure_ne_top _ _âŸ©
#align probability_theory.set_lintegral_cond_cdf ProbabilityTheory.set_lintegral_condCDF

theorem lintegral_condCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (x : â„) :
    âˆ«â» a, ENNReal.ofReal (condCDF Ï a x) âˆ‚Ï.fst = Ï (univ Ã—Ë¢ Iic x) := by
  rw [â† set_lintegral_univ, set_lintegral_condCDF Ï _ MeasurableSet.univ]
#align probability_theory.lintegral_cond_cdf ProbabilityTheory.lintegral_condCDF

/-- The conditional cdf is a strongly measurable function of `a : Î±` for all `x : â„`. -/
theorem stronglyMeasurable_condCDF (Ï : Measure (Î± Ã— â„)) (x : â„) :
    StronglyMeasurable fun a => condCDF Ï a x :=
  (measurable_condCDF Ï x).stronglyMeasurable
#align probability_theory.strongly_measurable_cond_cdf ProbabilityTheory.stronglyMeasurable_condCDF

theorem integrable_condCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (x : â„) :
    Integrable (fun a => condCDF Ï a x) Ï.fst := by
  refine' integrable_of_forall_fin_meas_le _ (measure_lt_top Ï.fst univ) _ fun t _ _ => _
  Â· exact (stronglyMeasurable_condCDF Ï _).aestronglyMeasurable
  Â· have : âˆ€ y, (â€–condCDF Ï y xâ€–â‚Š : â„â‰¥0âˆ) â‰¤ 1 := by
      intro y
      rw [Real.nnnorm_of_nonneg (condCDF_nonneg _ _ _)]
      -- Porting note: was exact_mod_cast condCDF_le_one _ _ _
      simp only [ENNReal.coe_le_one_iff]
      exact condCDF_le_one _ _ _
    refine'
      (set_lintegral_mono (measurable_condCDF _ _).ennnorm measurable_one fun y _ => this y).trans
        _
    simp only [Pi.one_apply, lintegral_one, Measure.restrict_apply, MeasurableSet.univ, univ_inter]
    exact measure_mono (subset_univ _)
#align probability_theory.integrable_cond_cdf ProbabilityTheory.integrable_condCDF

theorem set_integral_condCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (x : â„) {s : Set Î±}
    (hs : MeasurableSet s) : âˆ« a in s, condCDF Ï a x âˆ‚Ï.fst = (Ï (s Ã—Ë¢ Iic x)).toReal := by
  have h := set_lintegral_condCDF Ï x hs
  rw [â† ofReal_integral_eq_lintegral_ofReal] at h
  Â· rw [â† h, ENNReal.toReal_ofReal]
    exact integral_nonneg fun _ => condCDF_nonneg _ _ _
  Â· exact (integrable_condCDF _ _).integrableOn
  Â· exact eventually_of_forall fun _ => condCDF_nonneg _ _ _
#align probability_theory.set_integral_cond_cdf ProbabilityTheory.set_integral_condCDF

theorem integral_condCDF (Ï : Measure (Î± Ã— â„)) [IsFiniteMeasure Ï] (x : â„) :
    âˆ« a, condCDF Ï a x âˆ‚Ï.fst = (Ï (univ Ã—Ë¢ Iic x)).toReal := by
  rw [â† set_integral_condCDF Ï _ MeasurableSet.univ, Measure.restrict_univ]
#align probability_theory.integral_cond_cdf ProbabilityTheory.integral_condCDF

section Measure

theorem measure_condCDF_Iic (Ï : Measure (Î± Ã— â„)) (a : Î±) (x : â„) :
    (condCDF Ï a).measure (Iic x) = ENNReal.ofReal (condCDF Ï a x) := by
  rw [â† sub_zero (condCDF Ï a x)]
  exact (condCDF Ï a).measure_Iic (tendsto_condCDF_atBot Ï a) _
#align probability_theory.measure_cond_cdf_Iic ProbabilityTheory.measure_condCDF_Iic

theorem measure_condCDF_univ (Ï : Measure (Î± Ã— â„)) (a : Î±) : (condCDF Ï a).measure univ = 1 := by
  rw [â† ENNReal.ofReal_one, â† sub_zero (1 : â„)]
  exact StieltjesFunction.measure_univ _ (tendsto_condCDF_atBot Ï a) (tendsto_condCDF_atTop Ï a)
#align probability_theory.measure_cond_cdf_univ ProbabilityTheory.measure_condCDF_univ

instance instIsProbabilityMeasure (Ï : Measure (Î± Ã— â„)) (a : Î±) :
    IsProbabilityMeasure (condCDF Ï a).measure :=
  âŸ¨measure_condCDF_univ Ï aâŸ©

/-- The function `a â†¦ (condCDF Ï a).measure` is measurable. -/
theorem measurable_measure_condCDF (Ï : Measure (Î± Ã— â„)) :
    Measurable fun a => (condCDF Ï a).measure := by
  rw [Measure.measurable_measure]
  refine' fun s hs => ?_
  -- Porting note: supplied `C`
  refine' MeasurableSpace.induction_on_inter
    (C := fun s => Measurable fun b â†¦ StieltjesFunction.measure (condCDF Ï b) s)
    (borel_eq_generateFrom_Iic â„) isPiSystem_Iic _ _ _ _ hs
  Â· simp only [measure_empty, measurable_const]
  Â· rintro S âŸ¨u, rflâŸ©
    simp_rw [measure_condCDF_Iic Ï _ u]
    exact (measurable_condCDF Ï u).ennreal_ofReal
  Â· intro t ht ht_cd_meas
    have :
      (fun a => (condCDF Ï a).measure tá¶œ) =
        (fun a => (condCDF Ï a).measure univ) - fun a => (condCDF Ï a).measure t := by
      ext1 a
      rw [measure_compl ht (measure_ne_top (condCDF Ï a).measure _), Pi.sub_apply]
    simp_rw [this, measure_condCDF_univ Ï]
    exact Measurable.sub measurable_const ht_cd_meas
  Â· intro f hf_disj hf_meas hf_cd_meas
    simp_rw [measure_iUnion hf_disj hf_meas]
    exact Measurable.ennreal_tsum hf_cd_meas
#align probability_theory.measurable_measure_cond_cdf ProbabilityTheory.measurable_measure_condCDF

end Measure

end ProbabilityTheory
