/-
Copyright (c) 2024 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Probability.Kernel.CompProdEqIff
import Mathlib.Probability.Kernel.Composition.Lemmas
import Mathlib.Probability.Kernel.Disintegration.StandardBorel

/-!

# Posterior kernel

For `Î¼ : Measure Î©` (called prior measure), seen as a measure on a parameter, and a kernel
`Îº : Kernel Î© ğ“§` that gives the conditional distribution of "data" in `ğ“§` given the prior parameter,
we can get the distribution of the data with `Îº âˆ˜â‚˜ Î¼`, and the joint distribution of parameter and
data with `Î¼ âŠ—â‚˜ Îº : Measure (Î© Ã— ğ“§)`.

The posterior distribution of the parameter given the data is a Markov kernel `Îºâ€ Î¼ : Kernel ğ“§ Î©`
such that `(Îº âˆ˜â‚˜ Î¼) âŠ—â‚˜ Îºâ€ Î¼ = (Î¼ âŠ—â‚˜ Îº).map Prod.swap`. That is, the joint distribution of parameter
and data can be recovered from the distribution of the data and the posterior.

## Main definitions

* `posterior Îº Î¼`: posterior of a kernel `Îº` for a prior measure `Î¼`.

## Main statements

* `compProd_posterior_eq_map_swap`: the main property of the posterior,
  `(Îº âˆ˜â‚˜ Î¼) âŠ—â‚˜ Îºâ€ Î¼ = (Î¼ âŠ—â‚˜ Îº).map Prod.swap`.
* `ae_eq_posterior_of_compProd_eq`
* `posterior_comp_self`: `Îºâ€ Î¼ âˆ˜â‚˜ Îº âˆ˜â‚˜ Î¼ = Î¼`
* `posterior_posterior`: `(Îºâ€ Î¼)â€ (Îº âˆ˜â‚˜ Î¼) =áµ[Î¼] Îº`
* `posterior_comp`: `(Î· âˆ˜â‚– Îº)â€ Î¼ =áµ[Î· âˆ˜â‚˜ Îº âˆ˜â‚˜ Î¼] Îºâ€ Î¼ âˆ˜â‚– Î·â€ (Îº âˆ˜â‚˜ Î¼)`

* `posterior_eq_withDensity`: If `Îº Ï‰ â‰ª Îº âˆ˜â‚˜ Î¼` for `Î¼`-almost every `Ï‰`,
  then for `Îº âˆ˜â‚˜ Î¼`-almost every `x`,
  `Îºâ€ Î¼ x = Î¼.withDensity (fun Ï‰ â†¦ Îº.rnDeriv (Kernel.const _ (Îº âˆ˜â‚˜ Î¼)) Ï‰ x)`.
  The condition is true for countable `Î©`: see `absolutelyContinuous_comp_of_countable`.

## Notation

`Îºâ€ Î¼` denotes the posterior of `Îº` with respect to `Î¼`, `posterior Îº Î¼`.
`â€ ` can be typed as `\dag` or `\dagger`.

This notation emphasizes that the posterior is a kind of inverse of `Îº`, which we would want to
denote `Îºâ€ `, but we have to also specify the measure `Î¼`.

-/

open scoped ENNReal

open MeasureTheory

namespace ProbabilityTheory

variable {Î© ğ“§ ğ“¨ ğ“© : Type*} {mÎ© : MeasurableSpace Î©} {mğ“§ : MeasurableSpace ğ“§}
    {mğ“¨ : MeasurableSpace ğ“¨} {mğ“© : MeasurableSpace ğ“©}
    {Îº : Kernel Î© ğ“§} {Î¼ : Measure Î©} [IsFiniteMeasure Î¼] [IsFiniteKernel Îº]

variable [StandardBorelSpace Î©] [Nonempty Î©]

/-- Posterior of the kernel `Îº` with respect to the measure `Î¼`. -/
noncomputable
def posterior (Îº : Kernel Î© ğ“§) (Î¼ : Measure Î©) [IsFiniteMeasure Î¼] [IsFiniteKernel Îº] :
    Kernel ğ“§ Î© :=
  ((Î¼ âŠ—â‚˜ Îº).map Prod.swap).condKernel

/-- Posterior of the kernel `Îº` with respect to the measure `Î¼`. -/
scoped[ProbabilityTheory] infix:arg "â€ " => ProbabilityTheory.posterior

/-- The posterior is a Markov kernel. -/
instance : IsMarkovKernel Îºâ€ Î¼ := by rw [posterior]; infer_instance

/-- The main property of the posterior. -/
lemma compProd_posterior_eq_map_swap : (Îº âˆ˜â‚˜ Î¼) âŠ—â‚˜ Îºâ€ Î¼ = (Î¼ âŠ—â‚˜ Îº).map Prod.swap := by
  simpa using ((Î¼ âŠ—â‚˜ Îº).map Prod.swap).disintegrate ((Î¼ âŠ—â‚˜ Îº).map Prod.swap).condKernel

lemma compProd_posterior_eq_swap_comp : (Îº âˆ˜â‚˜ Î¼) âŠ—â‚˜ Îºâ€ Î¼ = Kernel.swap Î© ğ“§ âˆ˜â‚˜ Î¼ âŠ—â‚˜ Îº := by
  rw [compProd_posterior_eq_map_swap, Measure.swap_comp]

lemma swap_compProd_posterior : Kernel.swap ğ“§ Î© âˆ˜â‚˜ (Îº âˆ˜â‚˜ Î¼) âŠ—â‚˜ Îºâ€ Î¼ = Î¼ âŠ—â‚˜ Îº := by
  rw [compProd_posterior_eq_swap_comp, Measure.comp_assoc, Kernel.swap_swap, Measure.id_comp]

/-- The main property of the posterior, as equality of the following diagrams:
```
         -- id          -- Îº
Î¼ -- Îº -|        =  Î¼ -|
         -- Îºâ€ Î¼         -- id
``` -/
lemma parallelProd_posterior_comp_copy_comp :
    (Kernel.id âˆ¥â‚– Îºâ€ Î¼) âˆ˜â‚˜ Kernel.copy ğ“§ âˆ˜â‚˜ Îº âˆ˜â‚˜ Î¼
      = (Îº âˆ¥â‚– Kernel.id) âˆ˜â‚˜ Kernel.copy Î© âˆ˜â‚˜ Î¼ := by
  calc (Kernel.id âˆ¥â‚– Îºâ€ Î¼) âˆ˜â‚˜ Kernel.copy ğ“§ âˆ˜â‚˜ Îº âˆ˜â‚˜ Î¼
  _ = (Îº âˆ˜â‚˜ Î¼) âŠ—â‚˜ Îºâ€ Î¼ := by rw [â† Measure.compProd_eq_parallelComp_comp_copy_comp]
  _ = Kernel.swap _ _ âˆ˜â‚˜ (Î¼ âŠ—â‚˜ Îº) := by rw [compProd_posterior_eq_swap_comp]
  _ = Kernel.swap _ _ âˆ˜â‚˜ (Kernel.id âˆ¥â‚– Îº) âˆ˜â‚˜ Kernel.copy Î© âˆ˜â‚˜ Î¼ := by
    rw [Measure.compProd_eq_parallelComp_comp_copy_comp]
  _ = (Îº âˆ¥â‚– Kernel.id) âˆ˜â‚˜ Kernel.copy Î© âˆ˜â‚˜ Î¼ := by
    rw [Measure.comp_assoc, Kernel.swap_parallelComp, Measure.comp_assoc, Kernel.comp_assoc,
      Kernel.swap_copy, Measure.comp_assoc]

lemma posterior_prod_id_comp : (Îºâ€ Î¼ Ã—â‚– Kernel.id) âˆ˜â‚˜ Îº âˆ˜â‚˜ Î¼ = Î¼ âŠ—â‚˜ Îº := by
  rw [â† Kernel.swap_prod, â† Measure.comp_assoc, â† Measure.compProd_eq_comp_prod,
    compProd_posterior_eq_swap_comp, Measure.comp_assoc, Kernel.swap_swap, Measure.id_comp]

/-- The posterior is unique up to a `Îº âˆ˜â‚˜ Î¼`-null set. -/
lemma ae_eq_posterior_of_compProd_eq {Î· : Kernel ğ“§ Î©} [IsFiniteKernel Î·]
    (h : (Îº âˆ˜â‚˜ Î¼) âŠ—â‚˜ Î· = (Î¼ âŠ—â‚˜ Îº).map Prod.swap) :
    Î· =áµ[Îº âˆ˜â‚˜ Î¼] Îºâ€ Î¼ :=
  (Kernel.ae_eq_of_compProd_eq (compProd_posterior_eq_map_swap.trans h.symm)).symm

/-- The posterior is unique up to a `Îº âˆ˜â‚˜ Î¼`-null set. -/
lemma ae_eq_posterior_of_compProd_eq_swap_comp (Î· : Kernel ğ“§ Î©) [IsFiniteKernel Î·]
    (h : ((Îº âˆ˜â‚˜ Î¼) âŠ—â‚˜ Î·) = Kernel.swap Î© ğ“§ âˆ˜â‚˜ Î¼ âŠ—â‚˜ Îº) :
    Î· =áµ[Îº âˆ˜â‚˜ Î¼] Îºâ€ Î¼ :=
  ae_eq_posterior_of_compProd_eq <| by rw [h, Measure.swap_comp]

@[simp]
lemma posterior_comp_self [IsMarkovKernel Îº] : Îºâ€ Î¼ âˆ˜â‚˜ Îº âˆ˜â‚˜ Î¼ = Î¼ := by
  rw [â† Measure.snd_compProd, compProd_posterior_eq_map_swap, Measure.snd_map_swap,
    Measure.fst_compProd]

/-- The posterior of the identity kernel is the identity kernel. -/
lemma posterior_id (Î¼ : Measure Î©) [IsFiniteMeasure Î¼] : Kernel.idâ€ Î¼ =áµ[Î¼] Kernel.id := by
  suffices Kernel.id =áµ[Kernel.id âˆ˜â‚˜ Î¼] (Kernel.id : Kernel Î© Î©)â€ Î¼ by
    rw [Measure.id_comp] at this
    filter_upwards [this] with a ha using ha.symm
  refine ae_eq_posterior_of_compProd_eq_swap_comp Kernel.id ?_
  rw [Measure.id_comp, Measure.compProd_id_eq_copy_comp, Measure.comp_assoc, Kernel.swap_copy]

/-- For a deterministic kernel `Îº`, `Îº âˆ˜â‚– Îºâ€ Î¼` is `Î¼.map f`-a.e. equal to the identity kernel. -/
lemma deterministic_comp_posterior [MeasurableSpace.CountablyGenerated ğ“§]
    {f : Î© â†’ ğ“§} (hf : Measurable f) :
    Kernel.deterministic f hf âˆ˜â‚– (Kernel.deterministic f hf)â€ Î¼ =áµ[Î¼.map f] Kernel.id := by
  refine Kernel.ae_eq_of_compProd_eq ?_
  calc Î¼.map f âŠ—â‚˜ (Kernel.deterministic f hf âˆ˜â‚– (Kernel.deterministic f hf)â€ Î¼)
  _ = (Kernel.deterministic f hf âˆ˜â‚˜ Î¼)
      âŠ—â‚˜ (Kernel.deterministic f hf âˆ˜â‚– (Kernel.deterministic f hf)â€ Î¼) := by
    rw [Measure.deterministic_comp_eq_map]
  _ = (Kernel.id âˆ¥â‚– Kernel.deterministic f hf) âˆ˜â‚˜ (Kernel.id âˆ¥â‚– (Kernel.deterministic f hf)â€ Î¼) âˆ˜â‚˜
      Kernel.copy ğ“§ âˆ˜â‚˜ Kernel.deterministic f hf âˆ˜â‚˜ Î¼ := by
    rw [Measure.compProd_eq_parallelComp_comp_copy_comp,
      â† Kernel.parallelComp_id_left_comp_parallelComp, â† Measure.comp_assoc]
  _ = (Kernel.id âˆ¥â‚– Kernel.deterministic f hf) âˆ˜â‚˜ (Kernel.deterministic f hf âˆ¥â‚– Kernel.id) âˆ˜â‚˜
      Kernel.copy Î© âˆ˜â‚˜ Î¼ := by rw [parallelProd_posterior_comp_copy_comp]
  _ = (Kernel.deterministic f hf âˆ¥â‚– Kernel.deterministic f hf) âˆ˜â‚˜ Kernel.copy Î© âˆ˜â‚˜ Î¼ := by
    rw [Measure.comp_assoc, Kernel.parallelComp_comp_parallelComp, Kernel.id_comp, Kernel.comp_id]
  _ = (Kernel.copy ğ“§ âˆ˜â‚– Kernel.deterministic f hf) âˆ˜â‚˜ Î¼ := by -- `deterministic` is used here
    rw [Measure.comp_assoc, Kernel.deterministic_comp_copy]
  _ = Î¼.map f âŠ—â‚˜ Kernel.id := by
    rw [Measure.compProd_id_eq_copy_comp, â† Measure.comp_assoc,
      Measure.deterministic_comp_eq_map]

lemma absolutelyContinuous_posterior {Î½ : Measure ğ“§} [SFinite Î½] (h_ac : âˆ€áµ Ï‰ âˆ‚Î¼, Îº Ï‰ â‰ª Î½) :
    âˆ€áµ b âˆ‚(Îº âˆ˜â‚˜ Î¼), (Îºâ€ Î¼) b â‰ª Î¼ := by
  suffices (Îº âˆ˜â‚˜ Î¼) âŠ—â‚˜ (Îºâ€ Î¼) â‰ª Î½.prod Î¼ by
    rw [â† Measure.compProd_const] at this
    simpa using this.kernel_of_compProd
  suffices Î¼ âŠ—â‚˜ Îº â‰ª Î¼.prod Î½ by
    rw [compProd_posterior_eq_map_swap, â† Measure.prod_swap]
    exact this.map measurable_swap
  rw [â† Measure.compProd_const]
  refine Measure.AbsolutelyContinuous.compProd_right ?_
  simpa

section StandardBorelSpace

variable [StandardBorelSpace ğ“§] [Nonempty ğ“§]

/-- The posterior is involutive (up to `Î¼`-a.e. equality). -/
lemma posterior_posterior [IsMarkovKernel Îº] : (Îºâ€ Î¼)â€ (Îº âˆ˜â‚˜ Î¼) =áµ[Î¼] Îº := by
  suffices Îº =áµ[Îºâ€ Î¼ âˆ˜â‚˜ Îº âˆ˜â‚˜ Î¼] (Îºâ€ Î¼)â€ (Îº âˆ˜â‚˜ Î¼) by
    rw [posterior_comp_self] at this
    filter_upwards [this] with a h using h.symm
  refine ae_eq_posterior_of_compProd_eq_swap_comp Îº ?_
  rw [posterior_comp_self, compProd_posterior_eq_swap_comp, Measure.comp_assoc,
    Kernel.swap_swap, Measure.id_comp]

/-- The posterior is contravariant. -/
lemma posterior_comp {Î· : Kernel ğ“§ ğ“¨} [IsFiniteKernel Î·] :
    (Î· âˆ˜â‚– Îº)â€ Î¼ =áµ[Î· âˆ˜â‚˜ Îº âˆ˜â‚˜ Î¼] Îºâ€ Î¼ âˆ˜â‚– Î·â€ (Îº âˆ˜â‚˜ Î¼) := by
  rw [Measure.comp_assoc]
  refine (ae_eq_posterior_of_compProd_eq_swap_comp ((Îºâ€ Î¼) âˆ˜â‚– Î·â€ (Îº âˆ˜â‚˜ Î¼)) ?_).symm
  simp_rw [Measure.compProd_eq_comp_prod, â† Kernel.parallelComp_comp_copy,
    â† Kernel.parallelComp_id_left_comp_parallelComp, â† Measure.comp_assoc]
  calc (Kernel.id âˆ¥â‚– Îºâ€ Î¼) âˆ˜â‚˜ (Kernel.id âˆ¥â‚– Î·â€ (Îº âˆ˜â‚˜ Î¼)) âˆ˜â‚˜ (Kernel.copy ğ“¨) âˆ˜â‚˜ Î· âˆ˜â‚˜ Îº âˆ˜â‚˜ Î¼
  _ = (Kernel.id âˆ¥â‚– Îºâ€ Î¼) âˆ˜â‚˜ (Î· âˆ¥â‚– Kernel.id) âˆ˜â‚˜ Kernel.copy ğ“§ âˆ˜â‚˜ Îº âˆ˜â‚˜ Î¼ := by
    rw [parallelProd_posterior_comp_copy_comp]
  _ = (Î· âˆ¥â‚– Kernel.id) âˆ˜â‚˜ (Kernel.id âˆ¥â‚– Îºâ€ Î¼) âˆ˜â‚˜ Kernel.copy ğ“§ âˆ˜â‚˜ Îº âˆ˜â‚˜ Î¼ := by
    rw [Measure.comp_assoc, Kernel.parallelComp_comm, â† Measure.comp_assoc]
  _ = (Î· âˆ¥â‚– Kernel.id) âˆ˜â‚˜ (Îº âˆ¥â‚– Kernel.id) âˆ˜â‚˜ Kernel.copy Î© âˆ˜â‚˜ Î¼ := by
    rw [parallelProd_posterior_comp_copy_comp]
  _ = (Kernel.swap _ _) âˆ˜â‚˜ (Kernel.id âˆ¥â‚– Î·) âˆ˜â‚˜ (Kernel.id âˆ¥â‚– Îº) âˆ˜â‚˜ Kernel.copy Î© âˆ˜â‚˜ Î¼ := by
    simp_rw [Measure.comp_assoc]
    conv_rhs => rw [â† Kernel.comp_assoc]
    rw [Kernel.swap_parallelComp, Kernel.comp_assoc, â† Kernel.comp_assoc (Kernel.swap Î© ğ“§),
      Kernel.swap_parallelComp, Kernel.comp_assoc, Kernel.swap_copy]

end StandardBorelSpace


section CountableOrCountablyGenerated

variable [MeasurableSpace.CountableOrCountablyGenerated Î© ğ“§]

lemma absolutelyContinuous_of_posterior (h_ac : âˆ€áµ b âˆ‚(Îº âˆ˜â‚˜ Î¼), (Îºâ€ Î¼) b â‰ª Î¼) :
    âˆ€áµ Ï‰ âˆ‚Î¼, Îº Ï‰ â‰ª Îº âˆ˜â‚˜ Î¼ := by
  suffices Î¼ âŠ—â‚˜ Îº â‰ª Î¼.prod (Îº âˆ˜â‚˜ Î¼) by
    rw [â† Measure.compProd_const] at this
    simpa using this.kernel_of_compProd
  suffices (Îº âˆ˜â‚˜ Î¼) âŠ—â‚˜ Îºâ€ Î¼ â‰ª (Îº âˆ˜â‚˜ Î¼).prod Î¼ by
    rw [â† swap_compProd_posterior, â† Measure.prod_swap, Measure.swap_comp]
    exact this.map measurable_swap
  rw [â† Measure.compProd_const]
  refine Measure.AbsolutelyContinuous.compProd_right ?_
  simpa

lemma absolutelyContinuous_posterior_iff : (âˆ€áµ b âˆ‚(Îº âˆ˜â‚˜ Î¼), (Îºâ€ Î¼) b â‰ª Î¼) â†” âˆ€áµ Ï‰ âˆ‚Î¼, Îº Ï‰ â‰ª Îº âˆ˜â‚˜ Î¼ :=
  âŸ¨absolutelyContinuous_of_posterior, absolutelyContinuous_posteriorâŸ©

lemma Kernel.absolutelyContinuous_comp_of_absolutelyContinuous {Î½ : Measure ğ“§} [SFinite Î½]
    (h_ac : âˆ€áµ Ï‰ âˆ‚Î¼, Îº Ï‰ â‰ª Î½) :
    âˆ€áµ Ï‰ âˆ‚Î¼, Îº Ï‰ â‰ª Îº âˆ˜â‚˜ Î¼ := by
  rw [â† absolutelyContinuous_posterior_iff]
  exact absolutelyContinuous_posterior h_ac

lemma rnDeriv_posterior_ae_prod (h_ac : âˆ€áµ Ï‰ âˆ‚Î¼, Îº Ï‰ â‰ª Îº âˆ˜â‚˜ Î¼) :
    âˆ€áµ p âˆ‚(Î¼.prod (Îº âˆ˜â‚˜ Î¼)),
      (Îºâ€ Î¼).rnDeriv (Kernel.const _ Î¼) p.2 p.1 = Îº.rnDeriv (Kernel.const _ (Îº âˆ˜â‚˜ Î¼)) p.1 p.2 := by
  -- We prove the a.e. equality by showing that integrals on the Ï€-system of rectangles are equal.
  -- First, the integral of the left-hand side on `s Ã—Ë¢ t` is `(Î¼ âŠ—â‚˜ Îº) (s Ã—Ë¢ t)`, which we prove
  -- by showing that it's equal to `((Îº âˆ˜â‚˜ Î¼) âŠ— Îºâ€ Î¼) (t Ã—Ë¢ s)` and using the main property of the
  -- posterior.
  have h1 {s : Set Î©} {t : Set ğ“§} (hs : MeasurableSet s) (ht : MeasurableSet t) :
      âˆ«â» x in s Ã—Ë¢ t, (Îºâ€ Î¼).rnDeriv (Kernel.const _ Î¼) x.2 x.1 âˆ‚Î¼.prod (â‡‘Îº âˆ˜â‚˜ Î¼)
        = (Î¼ âŠ—â‚˜ Îº) (s Ã—Ë¢ t) := by
    rw [setLIntegral_prod_symm _ (by fun_prop), â† swap_compProd_posterior, Measure.swap_comp,
      Measure.map_apply measurable_swap (hs.prod ht), Set.preimage_swap_prod,
      Measure.compProd_apply_prod ht hs]
    refine lintegral_congr_ae <| ae_restrict_of_ae ?_
    filter_upwards [absolutelyContinuous_posterior h_ac] with x h_ac'
    change âˆ«â» Ï‰ in s, (Îºâ€ Î¼).rnDeriv (Kernel.const ğ“§ Î¼) x Ï‰ âˆ‚(Kernel.const ğ“§ Î¼ x) = _
    rw [Kernel.setLIntegral_rnDeriv h_ac' hs]
  have h2 {s : Set Î©} {t : Set ğ“§} (hs : MeasurableSet s) (ht : MeasurableSet t) :
  -- Second, the integral of the right-hand side on `s Ã—Ë¢ t` is `(Î¼ âŠ—â‚˜ Îº) (s Ã—Ë¢ t)`.
      âˆ«â» x in s Ã—Ë¢ t, Îº.rnDeriv (Kernel.const _ (Îº âˆ˜â‚˜ Î¼)) x.1 x.2 âˆ‚Î¼.prod (â‡‘Îº âˆ˜â‚˜ Î¼)
        = (Î¼ âŠ—â‚˜ Îº) (s Ã—Ë¢ t) := by
    rw [setLIntegral_prod _ (by fun_prop), Measure.compProd_apply_prod hs ht]
    refine lintegral_congr_ae <| ae_restrict_of_ae ?_
    filter_upwards [h_ac] with Ï‰ h_ac
    change âˆ«â» x in t, Îº.rnDeriv (Kernel.const Î© (Îº âˆ˜â‚˜ Î¼)) Ï‰ x âˆ‚(Kernel.const Î© (Îº âˆ˜â‚˜ Î¼) Ï‰) = _
    rw [Kernel.setLIntegral_rnDeriv h_ac ht]
  -- We extend from the Ï€-system to the Ïƒ-algebra.
  refine ae_eq_of_setLIntegral_prod_eq (by fun_prop) (by fun_prop) ?_ ?_
  Â· refine ne_of_lt ?_
    calc âˆ«â» x, (Îºâ€ Î¼).rnDeriv (Kernel.const _ Î¼) x.2 x.1 âˆ‚Î¼.prod (Îº âˆ˜â‚˜ Î¼)
    _ = (Î¼ âŠ—â‚˜ Îº) Set.univ := by rw [â† setLIntegral_univ, â† Set.univ_prod_univ, h1 .univ .univ]
    _ < âŠ¤ := measure_lt_top _ _
  Â· intro s hs t ht
    rw [h1 hs ht, h2 hs ht]

lemma rnDeriv_posterior (h_ac : âˆ€áµ Ï‰ âˆ‚Î¼, Îº Ï‰ â‰ª Îº âˆ˜â‚˜ Î¼) :
    âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€áµ x âˆ‚(Îº âˆ˜â‚˜ Î¼),
      (Îºâ€ Î¼).rnDeriv (Kernel.const _ Î¼) x Ï‰ = Îº.rnDeriv (Kernel.const _ (Îº âˆ˜â‚˜ Î¼)) Ï‰ x := by
  convert Measure.ae_ae_of_ae_prod (rnDeriv_posterior_ae_prod h_ac) -- much faster than `exact`

lemma rnDeriv_posterior_symm (h_ac : âˆ€áµ Ï‰ âˆ‚Î¼, Îº Ï‰ â‰ª Îº âˆ˜â‚˜ Î¼) :
    âˆ€áµ x âˆ‚(Îº âˆ˜â‚˜ Î¼), âˆ€áµ Ï‰ âˆ‚Î¼,
      (Îºâ€ Î¼).rnDeriv (Kernel.const _ Î¼) x Ï‰ = Îº.rnDeriv (Kernel.const _ (Îº âˆ˜â‚˜ Î¼)) Ï‰ x := by
  rw [Measure.ae_ae_comm]
  Â· exact rnDeriv_posterior h_ac
  Â· exact measurableSet_eq_fun' (by fun_prop) (by fun_prop)

/-- If `Îº Ï‰ â‰ª Îº âˆ˜â‚˜ Î¼` for `Î¼`-almost every `Ï‰`, then for `Îº âˆ˜â‚˜ Î¼`-almost every `x`,
`Îºâ€ Î¼ x = Î¼.withDensity (fun Ï‰ â†¦ Îº.rnDeriv (Kernel.const _ (Îº âˆ˜â‚˜ Î¼)) Ï‰ x)`.
This is a form of **Bayes' theorem**.
The condition is true for example for countable `Î©`. -/
lemma posterior_eq_withDensity (h_ac : âˆ€áµ Ï‰ âˆ‚Î¼, Îº Ï‰ â‰ª Îº âˆ˜â‚˜ Î¼) :
    âˆ€áµ x âˆ‚(Îº âˆ˜â‚˜ Î¼), (Îºâ€ Î¼) x = Î¼.withDensity (fun Ï‰ â†¦ Îº.rnDeriv (Kernel.const _ (Îº âˆ˜â‚˜ Î¼)) Ï‰ x) := by
  filter_upwards [rnDeriv_posterior_symm h_ac, absolutelyContinuous_posterior h_ac] with x h h_ac'
  ext s hs
  rw [â† Measure.setLIntegral_rnDeriv h_ac', withDensity_apply _ hs]
  refine setLIntegral_congr_fun_ae hs ?_
  filter_upwards [h, Kernel.rnDeriv_eq_rnDeriv_measure (Îº := Îºâ€ Î¼) (Î· := Kernel.const ğ“§ Î¼) (a := x)]
    with Ï‰ h h_eq hÏ‰s
  rw [â† h, h_eq, Kernel.const_apply]

lemma posterior_eq_withDensity_of_countable {Î© : Type*} [Countable Î©] [MeasurableSpace Î©]
    [Nonempty Î©] [StandardBorelSpace Î©] (Îº : Kernel Î© ğ“§) [IsFiniteKernel Îº]
    (Î¼ : Measure Î©) [IsFiniteMeasure Î¼] :
    âˆ€áµ x âˆ‚(Îº âˆ˜â‚˜ Î¼), (Îºâ€ Î¼) x = Î¼.withDensity (fun Ï‰ â†¦ (Îº Ï‰).rnDeriv (Îº âˆ˜â‚˜ Î¼) x) := by
  have h_rnDeriv Ï‰ := Kernel.rnDeriv_eq_rnDeriv_measure (Îº := Îº) (Î· := Kernel.const Î© (Îº âˆ˜â‚˜ Î¼))
    (a := Ï‰)
  simp only [Filter.EventuallyEq, Kernel.const_apply] at h_rnDeriv
  rw [â† ae_all_iff] at h_rnDeriv
  filter_upwards [posterior_eq_withDensity Measure.absolutelyContinuous_comp_of_countable,
    h_rnDeriv] with x hx hx_all
  simp_rw [hx, hx_all]

end CountableOrCountablyGenerated

end ProbabilityTheory
