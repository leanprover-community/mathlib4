/-
Copyright (c) 2023 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Probability.Kernel.Composition
import Mathlib.MeasureTheory.Integral.SetIntegral

#align_import probability.kernel.integral_comp_prod from "leanprover-community/mathlib"@"c0d694db494dd4f9aa57f2714b6e4c82b4ebc113"

/-!
# Bochner integral of a function against the composition-product of two kernels

We prove properties of the composition-product of two kernels. If `Îº` is an s-finite kernel from
`Î±` to `Î²` and `Î·` is an s-finite kernel from `Î± Ã— Î²` to `Î³`, we can form their composition-product
`Îº âŠ—â‚– Î· : kernel Î± (Î² Ã— Î³)`. We proved in `ProbabilityTheory.kernel.lintegral_compProd` that it
verifies `âˆ«â» bc, f bc âˆ‚((Îº âŠ—â‚– Î·) a) = âˆ«â» b, âˆ«â» c, f (b, c) âˆ‚(Î· (a, b)) âˆ‚(Îº a)`. In this file, we
prove the same equality for the Bochner integral.

## Main statements

* `ProbabilityTheory.integral_compProd`: the integral against the composition-product is
  `âˆ« z, f z âˆ‚((Îº âŠ—â‚– Î·) a) = âˆ« x, âˆ« y, f (x, y) âˆ‚(Î· (a, x)) âˆ‚(Îº a)`

## Implementation details

This file is to a large extent a copy of part of
`Mathlib/MeasureTheory/Constructions/Prod/Basic.lean`. The product of
two measures is a particular case of composition-product of kernels and it turns out that once the
measurablity of the Lebesgue integral of a kernel is proved, almost all proofs about integrals
against products of measures extend with minimal modifications to the composition-product of two
kernels.
-/


noncomputable section

open scoped Topology ENNReal MeasureTheory ProbabilityTheory

open Set Function Real ENNReal MeasureTheory Filter ProbabilityTheory ProbabilityTheory.kernel

variable {Î± Î² Î³ E : Type*} {mÎ± : MeasurableSpace Î±} {mÎ² : MeasurableSpace Î²}
  {mÎ³ : MeasurableSpace Î³} [NormedAddCommGroup E] {Îº : kernel Î± Î²} [IsSFiniteKernel Îº]
  {Î· : kernel (Î± Ã— Î²) Î³} [IsSFiniteKernel Î·] {a : Î±}

namespace ProbabilityTheory

theorem hasFiniteIntegral_prod_mk_left (a : Î±) {s : Set (Î² Ã— Î³)} (h2s : (Îº âŠ—â‚– Î·) a s â‰  âˆ) :
    HasFiniteIntegral (fun b => (Î· (a, b) (Prod.mk b â»Â¹' s)).toReal) (Îº a) := by
  let t := toMeasurable ((Îº âŠ—â‚– Î·) a) s
  simp_rw [HasFiniteIntegral, ennnorm_eq_ofReal toReal_nonneg]
  calc
    âˆ«â» b, ENNReal.ofReal (Î· (a, b) (Prod.mk b â»Â¹' s)).toReal âˆ‚Îº a
    _ â‰¤ âˆ«â» b, Î· (a, b) (Prod.mk b â»Â¹' t) âˆ‚Îº a := by
      refine lintegral_mono_ae ?_
      filter_upwards [ae_kernel_lt_top a h2s] with b hb
      rw [ofReal_toReal hb.ne]
      exact measure_mono (preimage_mono (subset_toMeasurable _ _))
    _ â‰¤ (Îº âŠ—â‚– Î·) a t := le_compProd_apply _ _ _ _
    _ = (Îº âŠ—â‚– Î·) a s := measure_toMeasurable s
    _ < âŠ¤ := h2s.lt_top
#align probability_theory.has_finite_integral_prod_mk_left ProbabilityTheory.hasFiniteIntegral_prod_mk_left

theorem integrable_kernel_prod_mk_left (a : Î±) {s : Set (Î² Ã— Î³)} (hs : MeasurableSet s)
    (h2s : (Îº âŠ—â‚– Î·) a s â‰  âˆ) : Integrable (fun b => (Î· (a, b) (Prod.mk b â»Â¹' s)).toReal) (Îº a) := by
  constructor
  Â· exact (measurable_kernel_prod_mk_left' hs a).ennreal_toReal.aestronglyMeasurable
  Â· exact hasFiniteIntegral_prod_mk_left a h2s
#align probability_theory.integrable_kernel_prod_mk_left ProbabilityTheory.integrable_kernel_prod_mk_left

theorem _root_.MeasureTheory.AEStronglyMeasurable.integral_kernel_compProd [NormedSpace â„ E]
    â¦ƒf : Î² Ã— Î³ â†’ Eâ¦„ (hf : AEStronglyMeasurable f ((Îº âŠ—â‚– Î·) a)) :
    AEStronglyMeasurable (fun x => âˆ« y, f (x, y) âˆ‚Î· (a, x)) (Îº a) :=
  âŸ¨fun x => âˆ« y, hf.mk f (x, y) âˆ‚Î· (a, x), hf.stronglyMeasurable_mk.integral_kernel_prod_right'', by
    filter_upwards [ae_ae_of_ae_compProd hf.ae_eq_mk] with _ hx using integral_congr_ae hxâŸ©
#align measure_theory.ae_strongly_measurable.integral_kernel_comp_prod MeasureTheory.AEStronglyMeasurable.integral_kernel_compProd

theorem _root_.MeasureTheory.AEStronglyMeasurable.compProd_mk_left {Î´ : Type*} [TopologicalSpace Î´]
    {f : Î² Ã— Î³ â†’ Î´} (hf : AEStronglyMeasurable f ((Îº âŠ—â‚– Î·) a)) :
    âˆ€áµ x âˆ‚Îº a, AEStronglyMeasurable (fun y => f (x, y)) (Î· (a, x)) := by
  filter_upwards [ae_ae_of_ae_compProd hf.ae_eq_mk] with x hx using
    âŸ¨fun y => hf.mk f (x, y), hf.stronglyMeasurable_mk.comp_measurable measurable_prod_mk_left, hxâŸ©
#align measure_theory.ae_strongly_measurable.comp_prod_mk_left MeasureTheory.AEStronglyMeasurable.compProd_mk_left

/-! ### Integrability -/


theorem hasFiniteIntegral_compProd_iff â¦ƒf : Î² Ã— Î³ â†’ Eâ¦„ (h1f : StronglyMeasurable f) :
    HasFiniteIntegral f ((Îº âŠ—â‚– Î·) a) â†”
      (âˆ€áµ x âˆ‚Îº a, HasFiniteIntegral (fun y => f (x, y)) (Î· (a, x))) âˆ§
        HasFiniteIntegral (fun x => âˆ« y, â€–f (x, y)â€– âˆ‚Î· (a, x)) (Îº a) := by
  simp only [HasFiniteIntegral]
  rw [kernel.lintegral_compProd _ _ _ h1f.ennnorm]
  have : âˆ€ x, âˆ€áµ y âˆ‚Î· (a, x), 0 â‰¤ â€–f (x, y)â€– := fun x => eventually_of_forall fun y => norm_nonneg _
  simp_rw [integral_eq_lintegral_of_nonneg_ae (this _)
      (h1f.norm.comp_measurable measurable_prod_mk_left).aestronglyMeasurable,
    ennnorm_eq_ofReal toReal_nonneg, ofReal_norm_eq_coe_nnnorm]
  have : âˆ€ {p q r : Prop} (_ : r â†’ p), (r â†” p âˆ§ q) â†” p â†’ (r â†” q) := fun {p q r} h1 => by
    rw [â† and_congr_right_iff, and_iff_right_of_imp h1]
  rw [this]
  Â· intro h2f; rw [lintegral_congr_ae]
    filter_upwards [h2f] with x hx
    rw [ofReal_toReal]; rw [â† lt_top_iff_ne_top]; exact hx
  Â· intro h2f; refine ae_lt_top ?_ h2f.ne; exact h1f.ennnorm.lintegral_kernel_prod_right''
#align probability_theory.has_finite_integral_comp_prod_iff ProbabilityTheory.hasFiniteIntegral_compProd_iff

theorem hasFiniteIntegral_compProd_iff' â¦ƒf : Î² Ã— Î³ â†’ Eâ¦„
    (h1f : AEStronglyMeasurable f ((Îº âŠ—â‚– Î·) a)) :
    HasFiniteIntegral f ((Îº âŠ—â‚– Î·) a) â†”
      (âˆ€áµ x âˆ‚Îº a, HasFiniteIntegral (fun y => f (x, y)) (Î· (a, x))) âˆ§
        HasFiniteIntegral (fun x => âˆ« y, â€–f (x, y)â€– âˆ‚Î· (a, x)) (Îº a) := by
  rw [hasFiniteIntegral_congr h1f.ae_eq_mk,
    hasFiniteIntegral_compProd_iff h1f.stronglyMeasurable_mk]
  apply and_congr
  Â· apply eventually_congr
    filter_upwards [ae_ae_of_ae_compProd h1f.ae_eq_mk.symm] with x hx using
      hasFiniteIntegral_congr hx
  Â· apply hasFiniteIntegral_congr
    filter_upwards [ae_ae_of_ae_compProd h1f.ae_eq_mk.symm] with _ hx using
      integral_congr_ae (EventuallyEq.fun_comp hx _)
#align probability_theory.has_finite_integral_comp_prod_iff' ProbabilityTheory.hasFiniteIntegral_compProd_iff'

theorem integrable_compProd_iff â¦ƒf : Î² Ã— Î³ â†’ Eâ¦„ (hf : AEStronglyMeasurable f ((Îº âŠ—â‚– Î·) a)) :
    Integrable f ((Îº âŠ—â‚– Î·) a) â†”
      (âˆ€áµ x âˆ‚Îº a, Integrable (fun y => f (x, y)) (Î· (a, x))) âˆ§
        Integrable (fun x => âˆ« y, â€–f (x, y)â€– âˆ‚Î· (a, x)) (Îº a) := by
  simp only [Integrable, hasFiniteIntegral_compProd_iff' hf, hf.norm.integral_kernel_compProd,
    hf, hf.compProd_mk_left, eventually_and, true_and_iff]
#align probability_theory.integrable_comp_prod_iff ProbabilityTheory.integrable_compProd_iff

theorem _root_.MeasureTheory.Integrable.compProd_mk_left_ae â¦ƒf : Î² Ã— Î³ â†’ Eâ¦„
    (hf : Integrable f ((Îº âŠ—â‚– Î·) a)) : âˆ€áµ x âˆ‚Îº a, Integrable (fun y => f (x, y)) (Î· (a, x)) :=
  ((integrable_compProd_iff hf.aestronglyMeasurable).mp hf).1
#align measure_theory.integrable.comp_prod_mk_left_ae MeasureTheory.Integrable.compProd_mk_left_ae

theorem _root_.MeasureTheory.Integrable.integral_norm_compProd â¦ƒf : Î² Ã— Î³ â†’ Eâ¦„
    (hf : Integrable f ((Îº âŠ—â‚– Î·) a)) : Integrable (fun x => âˆ« y, â€–f (x, y)â€– âˆ‚Î· (a, x)) (Îº a) :=
  ((integrable_compProd_iff hf.aestronglyMeasurable).mp hf).2
#align measure_theory.integrable.integral_norm_comp_prod MeasureTheory.Integrable.integral_norm_compProd

theorem _root_.MeasureTheory.Integrable.integral_compProd [NormedSpace â„ E]
    â¦ƒf : Î² Ã— Î³ â†’ Eâ¦„ (hf : Integrable f ((Îº âŠ—â‚– Î·) a)) :
    Integrable (fun x => âˆ« y, f (x, y) âˆ‚Î· (a, x)) (Îº a) :=
  Integrable.mono hf.integral_norm_compProd hf.aestronglyMeasurable.integral_kernel_compProd <|
    eventually_of_forall fun x =>
      (norm_integral_le_integral_norm _).trans_eq <|
        (norm_of_nonneg <|
            integral_nonneg_of_ae <|
              eventually_of_forall fun y => (norm_nonneg (f (x, y)) : _)).symm
#align measure_theory.integrable.integral_comp_prod MeasureTheory.Integrable.integral_compProd

/-! ### Bochner integral -/


variable [NormedSpace â„ E] {E' : Type*} [NormedAddCommGroup E']
  [CompleteSpace E'] [NormedSpace â„ E']

theorem kernel.integral_fn_integral_add â¦ƒf g : Î² Ã— Î³ â†’ Eâ¦„ (F : E â†’ E')
    (hf : Integrable f ((Îº âŠ—â‚– Î·) a)) (hg : Integrable g ((Îº âŠ—â‚– Î·) a)) :
    âˆ« x, F (âˆ« y, f (x, y) + g (x, y) âˆ‚Î· (a, x)) âˆ‚Îº a =
      âˆ« x, F (âˆ« y, f (x, y) âˆ‚Î· (a, x) + âˆ« y, g (x, y) âˆ‚Î· (a, x)) âˆ‚Îº a := by
  refine integral_congr_ae ?_
  filter_upwards [hf.compProd_mk_left_ae, hg.compProd_mk_left_ae] with _ h2f h2g
  simp [integral_add h2f h2g]
#align probability_theory.kernel.integral_fn_integral_add ProbabilityTheory.kernel.integral_fn_integral_add

theorem kernel.integral_fn_integral_sub â¦ƒf g : Î² Ã— Î³ â†’ Eâ¦„ (F : E â†’ E')
    (hf : Integrable f ((Îº âŠ—â‚– Î·) a)) (hg : Integrable g ((Îº âŠ—â‚– Î·) a)) :
    âˆ« x, F (âˆ« y, f (x, y) - g (x, y) âˆ‚Î· (a, x)) âˆ‚Îº a =
      âˆ« x, F (âˆ« y, f (x, y) âˆ‚Î· (a, x) - âˆ« y, g (x, y) âˆ‚Î· (a, x)) âˆ‚Îº a := by
  refine integral_congr_ae ?_
  filter_upwards [hf.compProd_mk_left_ae, hg.compProd_mk_left_ae] with _ h2f h2g
  simp [integral_sub h2f h2g]
#align probability_theory.kernel.integral_fn_integral_sub ProbabilityTheory.kernel.integral_fn_integral_sub

theorem kernel.lintegral_fn_integral_sub â¦ƒf g : Î² Ã— Î³ â†’ Eâ¦„ (F : E â†’ â„â‰¥0âˆ)
    (hf : Integrable f ((Îº âŠ—â‚– Î·) a)) (hg : Integrable g ((Îº âŠ—â‚– Î·) a)) :
    âˆ«â» x, F (âˆ« y, f (x, y) - g (x, y) âˆ‚Î· (a, x)) âˆ‚Îº a =
      âˆ«â» x, F (âˆ« y, f (x, y) âˆ‚Î· (a, x) - âˆ« y, g (x, y) âˆ‚Î· (a, x)) âˆ‚Îº a := by
  refine lintegral_congr_ae ?_
  filter_upwards [hf.compProd_mk_left_ae, hg.compProd_mk_left_ae] with _ h2f h2g
  simp [integral_sub h2f h2g]
#align probability_theory.kernel.lintegral_fn_integral_sub ProbabilityTheory.kernel.lintegral_fn_integral_sub

theorem kernel.integral_integral_add â¦ƒf g : Î² Ã— Î³ â†’ Eâ¦„ (hf : Integrable f ((Îº âŠ—â‚– Î·) a))
    (hg : Integrable g ((Îº âŠ—â‚– Î·) a)) :
    âˆ« x, âˆ« y, f (x, y) + g (x, y) âˆ‚Î· (a, x) âˆ‚Îº a =
      âˆ« x, âˆ« y, f (x, y) âˆ‚Î· (a, x) âˆ‚Îº a + âˆ« x, âˆ« y, g (x, y) âˆ‚Î· (a, x) âˆ‚Îº a :=
  (kernel.integral_fn_integral_add id hf hg).trans <|
    integral_add hf.integral_compProd hg.integral_compProd
#align probability_theory.kernel.integral_integral_add ProbabilityTheory.kernel.integral_integral_add

theorem kernel.integral_integral_add' â¦ƒf g : Î² Ã— Î³ â†’ Eâ¦„ (hf : Integrable f ((Îº âŠ—â‚– Î·) a))
    (hg : Integrable g ((Îº âŠ—â‚– Î·) a)) :
    âˆ« x, âˆ« y, (f + g) (x, y) âˆ‚Î· (a, x) âˆ‚Îº a =
      âˆ« x, âˆ« y, f (x, y) âˆ‚Î· (a, x) âˆ‚Îº a + âˆ« x, âˆ« y, g (x, y) âˆ‚Î· (a, x) âˆ‚Îº a :=
  kernel.integral_integral_add hf hg
#align probability_theory.kernel.integral_integral_add' ProbabilityTheory.kernel.integral_integral_add'

theorem kernel.integral_integral_sub â¦ƒf g : Î² Ã— Î³ â†’ Eâ¦„ (hf : Integrable f ((Îº âŠ—â‚– Î·) a))
    (hg : Integrable g ((Îº âŠ—â‚– Î·) a)) :
    âˆ« x, âˆ« y, f (x, y) - g (x, y) âˆ‚Î· (a, x) âˆ‚Îº a =
      âˆ« x, âˆ« y, f (x, y) âˆ‚Î· (a, x) âˆ‚Îº a - âˆ« x, âˆ« y, g (x, y) âˆ‚Î· (a, x) âˆ‚Îº a :=
  (kernel.integral_fn_integral_sub id hf hg).trans <|
    integral_sub hf.integral_compProd hg.integral_compProd
#align probability_theory.kernel.integral_integral_sub ProbabilityTheory.kernel.integral_integral_sub

theorem kernel.integral_integral_sub' â¦ƒf g : Î² Ã— Î³ â†’ Eâ¦„ (hf : Integrable f ((Îº âŠ—â‚– Î·) a))
    (hg : Integrable g ((Îº âŠ—â‚– Î·) a)) :
    âˆ« x, âˆ« y, (f - g) (x, y) âˆ‚Î· (a, x) âˆ‚Îº a =
      âˆ« x, âˆ« y, f (x, y) âˆ‚Î· (a, x) âˆ‚Îº a - âˆ« x, âˆ« y, g (x, y) âˆ‚Î· (a, x) âˆ‚Îº a :=
  kernel.integral_integral_sub hf hg
#align probability_theory.kernel.integral_integral_sub' ProbabilityTheory.kernel.integral_integral_sub'

-- Porting note: couldn't get the `â†’â‚[]` syntax to work
theorem kernel.continuous_integral_integral :
    -- Continuous fun f : Î± Ã— Î² â†’â‚[(Îº âŠ—â‚– Î·) a] E => âˆ« x, âˆ« y, f (x, y) âˆ‚Î· (a, x) âˆ‚Îº a := by
    Continuous fun f : (MeasureTheory.Lp (Î± := Î² Ã— Î³) E 1 (((Îº âŠ—â‚– Î·) a) : Measure (Î² Ã— Î³))) =>
        âˆ« x, âˆ« y, f (x, y) âˆ‚Î· (a, x) âˆ‚Îº a := by
  rw [continuous_iff_continuousAt]; intro g
  refine
    tendsto_integral_of_L1 _ (L1.integrable_coeFn g).integral_compProd
      (eventually_of_forall fun h => (L1.integrable_coeFn h).integral_compProd) ?_
  simp_rw [â†
    kernel.lintegral_fn_integral_sub (fun x => (â€–xâ€–â‚Š : â„â‰¥0âˆ)) (L1.integrable_coeFn _)
      (L1.integrable_coeFn g)]
  apply tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds _ (fun i => zero_le _) _
  Â· exact fun i => âˆ«â» x, âˆ«â» y, â€–i (x, y) - g (x, y)â€–â‚Š âˆ‚Î· (a, x) âˆ‚Îº a
  swap; Â· exact fun i => lintegral_mono fun x => ennnorm_integral_le_lintegral_ennnorm _
  show
    Tendsto
      (fun i : Î² Ã— Î³ â†’â‚[(Îº âŠ—â‚– Î·) a] E => âˆ«â» x, âˆ«â» y : Î³, â€–i (x, y) - g (x, y)â€–â‚Š âˆ‚Î· (a, x) âˆ‚Îº a)
      (ğ“ g) (ğ“ 0)
  have : âˆ€ i : (MeasureTheory.Lp (Î± := Î² Ã— Î³) E 1 (((Îº âŠ—â‚– Î·) a) : Measure (Î² Ã— Î³))),
      Measurable fun z => (â€–i z - g zâ€–â‚Š : â„â‰¥0âˆ) := fun i =>
    ((Lp.stronglyMeasurable i).sub (Lp.stronglyMeasurable g)).ennnorm
  simp_rw [â† kernel.lintegral_compProd _ _ _ (this _), â† L1.ofReal_norm_sub_eq_lintegral, â†
    ofReal_zero]
  refine (continuous_ofReal.tendsto 0).comp ?_
  rw [â† tendsto_iff_norm_sub_tendsto_zero]
  exact tendsto_id
#align probability_theory.kernel.continuous_integral_integral ProbabilityTheory.kernel.continuous_integral_integral

theorem integral_compProd :
    âˆ€ {f : Î² Ã— Î³ â†’ E} (_ : Integrable f ((Îº âŠ—â‚– Î·) a)),
      âˆ« z, f z âˆ‚(Îº âŠ—â‚– Î·) a = âˆ« x, âˆ« y, f (x, y) âˆ‚Î· (a, x) âˆ‚Îº a := by
  by_cases hE : CompleteSpace E; swap
  Â· simp [integral, hE]
  apply Integrable.induction
  Â· intro c s hs h2s
    simp_rw [integral_indicator hs, â† indicator_comp_right, Function.comp,
      integral_indicator (measurable_prod_mk_left hs), MeasureTheory.setIntegral_const,
      integral_smul_const]
    congr 1
    rw [integral_toReal]
    rotate_left
    Â· exact (kernel.measurable_kernel_prod_mk_left' hs _).aemeasurable
    Â· exact ae_kernel_lt_top a h2s.ne
    rw [kernel.compProd_apply _ _ _ hs]
    rfl
  Â· intro f g _ i_f i_g hf hg
    simp_rw [integral_add' i_f i_g, kernel.integral_integral_add' i_f i_g, hf, hg]
  Â· exact isClosed_eq continuous_integral kernel.continuous_integral_integral
  Â· intro f g hfg _ hf
    convert hf using 1
    Â· exact integral_congr_ae hfg.symm
    Â· apply integral_congr_ae
      filter_upwards [ae_ae_of_ae_compProd hfg] with x hfgx using
        integral_congr_ae (ae_eq_symm hfgx)
#align probability_theory.integral_comp_prod ProbabilityTheory.integral_compProd

theorem setIntegral_compProd {f : Î² Ã— Î³ â†’ E} {s : Set Î²} {t : Set Î³} (hs : MeasurableSet s)
    (ht : MeasurableSet t) (hf : IntegrableOn f (s Ã—Ë¢ t) ((Îº âŠ—â‚– Î·) a)) :
    âˆ« z in s Ã—Ë¢ t, f z âˆ‚(Îº âŠ—â‚– Î·) a = âˆ« x in s, âˆ« y in t, f (x, y) âˆ‚Î· (a, x) âˆ‚Îº a := by
  -- Porting note: `compProd_restrict` needed some explicit argumnts
  rw [â† kernel.restrict_apply (Îº âŠ—â‚– Î·) (hs.prod ht), â† compProd_restrict hs ht, integral_compProd]
  Â· simp_rw [kernel.restrict_apply]
  Â· rw [compProd_restrict, kernel.restrict_apply]; exact hf
#align probability_theory.set_integral_comp_prod ProbabilityTheory.setIntegral_compProd

@[deprecated (since := "2024-04-17")]
alias set_integral_compProd := setIntegral_compProd

theorem setIntegral_compProd_univ_right (f : Î² Ã— Î³ â†’ E) {s : Set Î²} (hs : MeasurableSet s)
    (hf : IntegrableOn f (s Ã—Ë¢ univ) ((Îº âŠ—â‚– Î·) a)) :
    âˆ« z in s Ã—Ë¢ univ, f z âˆ‚(Îº âŠ—â‚– Î·) a = âˆ« x in s, âˆ« y, f (x, y) âˆ‚Î· (a, x) âˆ‚Îº a := by
  simp_rw [setIntegral_compProd hs MeasurableSet.univ hf, Measure.restrict_univ]
#align probability_theory.set_integral_comp_prod_univ_right ProbabilityTheory.setIntegral_compProd_univ_right

@[deprecated (since := "2024-04-17")]
alias set_integral_compProd_univ_right := setIntegral_compProd_univ_right

theorem setIntegral_compProd_univ_left (f : Î² Ã— Î³ â†’ E) {t : Set Î³} (ht : MeasurableSet t)
    (hf : IntegrableOn f (univ Ã—Ë¢ t) ((Îº âŠ—â‚– Î·) a)) :
    âˆ« z in univ Ã—Ë¢ t, f z âˆ‚(Îº âŠ—â‚– Î·) a = âˆ« x, âˆ« y in t, f (x, y) âˆ‚Î· (a, x) âˆ‚Îº a := by
  simp_rw [setIntegral_compProd MeasurableSet.univ ht hf, Measure.restrict_univ]
#align probability_theory.set_integral_comp_prod_univ_left ProbabilityTheory.setIntegral_compProd_univ_left

@[deprecated (since := "2024-04-17")]
alias set_integral_compProd_univ_left := setIntegral_compProd_univ_left

end ProbabilityTheory
