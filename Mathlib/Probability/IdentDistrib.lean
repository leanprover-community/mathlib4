/-
Copyright (c) 2022 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Probability.Variance
import Mathlib.MeasureTheory.Function.UniformIntegrable

/-!
# Identically distributed random variables

Two random variables defined on two (possibly different) probability spaces but taking value in
the same space are *identically distributed* if their distributions (i.e., the image probability
measures on the target space) coincide. We define this concept and establish its basic properties
in this file.

## Main definitions and results

* `IdentDistrib f g Î¼ Î½` registers that the image of `Î¼` under `f` coincides with the image of `Î½`
  under `g` (and that `f` and `g` are almost everywhere measurable, as otherwise the image measures
  don't make sense). The measures can be kept implicit as in `IdentDistrib f g` if the spaces
  are registered as measure spaces.
* `IdentDistrib.comp`: being identically distributed is stable under composition with measurable
  maps.

There are two main kinds of lemmas, under the assumption that `f` and `g` are identically
distributed: lemmas saying that two quantities computed for `f` and `g` are the same, and lemmas
saying that if `f` has some property then `g` also has it. The first kind is registered as
`IdentDistrib.foo_fst`, the second one as `IdentDistrib.foo_snd` (in the latter case, to deduce
a property of `f` from one of `g`, use `h.symm.foo_snd` where `h : IdentDistrib f g Î¼ Î½`). For
instance:

* `IdentDistrib.measure_mem_eq`: if `f` and `g` are identically distributed, then the probabilities
  that they belong to a given measurable set are the same.
* `IdentDistrib.integral_eq`: if `f` and `g` are identically distributed, then their integrals
  are the same.
* `IdentDistrib.variance_eq`: if `f` and `g` are identically distributed, then their variances
  are the same.

* `IdentDistrib.aestronglyMeasurable_snd`: if `f` and `g` are identically distributed and `f`
  is almost everywhere strongly measurable, then so is `g`.
* `IdentDistrib.memLp_snd`: if `f` and `g` are identically distributed and `f`
  belongs to `â„’p`, then so does `g`.

We also register several dot notation shortcuts for convenience.
For instance, if `h : IdentDistrib f g Î¼ Î½`, then `h.sq` states that `f^2` and `g^2` are
identically distributed, and `h.norm` states that `â€–fâ€–` and `â€–gâ€–` are identically distributed, and
so on.
-/


open MeasureTheory Filter Finset

noncomputable section

open scoped Topology MeasureTheory ENNReal NNReal

variable {Î± Î² Î³ Î´ : Type*} [MeasurableSpace Î±] [MeasurableSpace Î²] [MeasurableSpace Î³]
  [MeasurableSpace Î´]

namespace ProbabilityTheory

/-- Two functions defined on two (possibly different) measure spaces are identically distributed if
their image measures coincide. This only makes sense when the functions are ae measurable
(as otherwise the image measures are not defined), so we require this as well in the definition. -/
structure IdentDistrib (f : Î± â†’ Î³) (g : Î² â†’ Î³)
    (Î¼ : Measure Î± := by volume_tac)
    (Î½ : Measure Î² := by volume_tac) : Prop where
  aemeasurable_fst : AEMeasurable f Î¼
  aemeasurable_snd : AEMeasurable g Î½
  map_eq : Measure.map f Î¼ = Measure.map g Î½

namespace IdentDistrib

open TopologicalSpace

variable {Î¼ : Measure Î±} {Î½ : Measure Î²} {f : Î± â†’ Î³} {g : Î² â†’ Î³}

protected theorem refl (hf : AEMeasurable f Î¼) : IdentDistrib f f Î¼ Î¼ :=
  { aemeasurable_fst := hf
    aemeasurable_snd := hf
    map_eq := rfl }

protected theorem symm (h : IdentDistrib f g Î¼ Î½) : IdentDistrib g f Î½ Î¼ :=
  { aemeasurable_fst := h.aemeasurable_snd
    aemeasurable_snd := h.aemeasurable_fst
    map_eq := h.map_eq.symm }

protected theorem trans {Ï : Measure Î´} {h : Î´ â†’ Î³} (hâ‚ : IdentDistrib f g Î¼ Î½)
    (hâ‚‚ : IdentDistrib g h Î½ Ï) : IdentDistrib f h Î¼ Ï :=
  { aemeasurable_fst := hâ‚.aemeasurable_fst
    aemeasurable_snd := hâ‚‚.aemeasurable_snd
    map_eq := hâ‚.map_eq.trans hâ‚‚.map_eq }

protected theorem comp_of_aemeasurable {u : Î³ â†’ Î´} (h : IdentDistrib f g Î¼ Î½)
    (hu : AEMeasurable u (Measure.map f Î¼)) : IdentDistrib (u âˆ˜ f) (u âˆ˜ g) Î¼ Î½ :=
  { aemeasurable_fst := hu.comp_aemeasurable h.aemeasurable_fst
    aemeasurable_snd := by rw [h.map_eq] at hu; exact hu.comp_aemeasurable h.aemeasurable_snd
    map_eq := by
      rw [â† AEMeasurable.map_map_of_aemeasurable hu h.aemeasurable_fst, â†
        AEMeasurable.map_map_of_aemeasurable _ h.aemeasurable_snd, h.map_eq]
      rwa [â† h.map_eq] }

protected theorem comp {u : Î³ â†’ Î´} (h : IdentDistrib f g Î¼ Î½) (hu : Measurable u) :
    IdentDistrib (u âˆ˜ f) (u âˆ˜ g) Î¼ Î½ :=
  h.comp_of_aemeasurable hu.aemeasurable

protected theorem of_ae_eq {g : Î± â†’ Î³} (hf : AEMeasurable f Î¼) (heq : f =áµ[Î¼] g) :
    IdentDistrib f g Î¼ Î¼ :=
  { aemeasurable_fst := hf
    aemeasurable_snd := hf.congr heq
    map_eq := Measure.map_congr heq }

lemma _root_.MeasureTheory.AEMeasurable.identDistrib_mk
    (hf : AEMeasurable f Î¼) : IdentDistrib f (hf.mk f) Î¼ Î¼ :=
  IdentDistrib.of_ae_eq hf hf.ae_eq_mk

lemma _root_.MeasureTheory.AEStronglyMeasurable.identDistrib_mk
    [TopologicalSpace Î³] [PseudoMetrizableSpace Î³] [BorelSpace Î³]
    (hf : AEStronglyMeasurable f Î¼) : IdentDistrib f (hf.mk f) Î¼ Î¼ :=
  IdentDistrib.of_ae_eq hf.aemeasurable hf.ae_eq_mk

theorem measure_mem_eq (h : IdentDistrib f g Î¼ Î½) {s : Set Î³} (hs : MeasurableSet s) :
    Î¼ (f â»Â¹' s) = Î½ (g â»Â¹' s) := by
  rw [â† Measure.map_apply_of_aemeasurable h.aemeasurable_fst hs, â†
    Measure.map_apply_of_aemeasurable h.aemeasurable_snd hs, h.map_eq]

alias measure_preimage_eq := measure_mem_eq

theorem ae_snd (h : IdentDistrib f g Î¼ Î½) {p : Î³ â†’ Prop} (pmeas : MeasurableSet {x | p x})
    (hp : âˆ€áµ x âˆ‚Î¼, p (f x)) : âˆ€áµ x âˆ‚Î½, p (g x) := by
  apply (ae_map_iff h.aemeasurable_snd pmeas).1
  rw [â† h.map_eq]
  exact (ae_map_iff h.aemeasurable_fst pmeas).2 hp

theorem ae_mem_snd (h : IdentDistrib f g Î¼ Î½) {t : Set Î³} (tmeas : MeasurableSet t)
    (ht : âˆ€áµ x âˆ‚Î¼, f x âˆˆ t) : âˆ€áµ x âˆ‚Î½, g x âˆˆ t :=
  h.ae_snd tmeas ht

/-- In a second countable topology, the first function in an identically distributed pair is a.e.
strongly measurable. So is the second function, but use `h.symm.aestronglyMeasurable_fst` as
`h.aestronglyMeasurable_snd` has a different meaning. -/
theorem aestronglyMeasurable_fst [TopologicalSpace Î³] [MetrizableSpace Î³] [OpensMeasurableSpace Î³]
    [SecondCountableTopology Î³] (h : IdentDistrib f g Î¼ Î½) : AEStronglyMeasurable f Î¼ :=
  h.aemeasurable_fst.aestronglyMeasurable

/-- If `f` and `g` are identically distributed and `f` is a.e. strongly measurable, so is `g`. -/
theorem aestronglyMeasurable_snd [TopologicalSpace Î³] [MetrizableSpace Î³] [BorelSpace Î³]
    (h : IdentDistrib f g Î¼ Î½) (hf : AEStronglyMeasurable f Î¼) : AEStronglyMeasurable g Î½ := by
  refine aestronglyMeasurable_iff_aemeasurable_separable.2 âŸ¨h.aemeasurable_snd, ?_âŸ©
  rcases (aestronglyMeasurable_iff_aemeasurable_separable.1 hf).2 with âŸ¨t, t_sep, htâŸ©
  refine âŸ¨closure t, t_sep.closure, ?_âŸ©
  apply h.ae_mem_snd isClosed_closure.measurableSet
  filter_upwards [ht] with x hx using subset_closure hx

theorem aestronglyMeasurable_iff [TopologicalSpace Î³] [MetrizableSpace Î³] [BorelSpace Î³]
    (h : IdentDistrib f g Î¼ Î½) : AEStronglyMeasurable f Î¼ â†” AEStronglyMeasurable g Î½ :=
  âŸ¨fun hf => h.aestronglyMeasurable_snd hf, fun hg => h.symm.aestronglyMeasurable_snd hgâŸ©

theorem essSup_eq [ConditionallyCompleteLinearOrder Î³] [TopologicalSpace Î³] [OpensMeasurableSpace Î³]
    [OrderClosedTopology Î³] (h : IdentDistrib f g Î¼ Î½) : essSup f Î¼ = essSup g Î½ := by
  have I : âˆ€ a, Î¼ {x : Î± | a < f x} = Î½ {x : Î² | a < g x} := fun a =>
    h.measure_mem_eq measurableSet_Ioi
  simp_rw [essSup_eq_sInf, I]

theorem lintegral_eq {f : Î± â†’ â„â‰¥0âˆž} {g : Î² â†’ â„â‰¥0âˆž} (h : IdentDistrib f g Î¼ Î½) :
    âˆ«â» x, f x âˆ‚Î¼ = âˆ«â» x, g x âˆ‚Î½ := by
  change âˆ«â» x, id (f x) âˆ‚Î¼ = âˆ«â» x, id (g x) âˆ‚Î½
  rw [â† lintegral_map' aemeasurable_id h.aemeasurable_fst, â†
    lintegral_map' aemeasurable_id h.aemeasurable_snd, h.map_eq]

theorem integral_eq [NormedAddCommGroup Î³] [NormedSpace â„ Î³] [BorelSpace Î³]
    (h : IdentDistrib f g Î¼ Î½) : âˆ« x, f x âˆ‚Î¼ = âˆ« x, g x âˆ‚Î½ := by
  by_cases hf : AEStronglyMeasurable f Î¼
  Â· have A : AEStronglyMeasurable id (Measure.map f Î¼) := by
      rw [aestronglyMeasurable_iff_aemeasurable_separable]
      rcases (aestronglyMeasurable_iff_aemeasurable_separable.1 hf).2 with âŸ¨t, t_sep, htâŸ©
      refine âŸ¨aemeasurable_id, âŸ¨closure t, t_sep.closure, ?_âŸ©âŸ©
      rw [ae_map_iff h.aemeasurable_fst]
      Â· filter_upwards [ht] with x hx using subset_closure hx
      Â· exact isClosed_closure.measurableSet
    change âˆ« x, id (f x) âˆ‚Î¼ = âˆ« x, id (g x) âˆ‚Î½
    rw [â† integral_map h.aemeasurable_fst A]
    rw [h.map_eq] at A
    rw [â† integral_map h.aemeasurable_snd A, h.map_eq]
  Â· rw [integral_non_aestronglyMeasurable hf]
    rw [h.aestronglyMeasurable_iff] at hf
    rw [integral_non_aestronglyMeasurable hf]

theorem eLpNorm_eq [NormedAddCommGroup Î³] [OpensMeasurableSpace Î³] (h : IdentDistrib f g Î¼ Î½)
    (p : â„â‰¥0âˆž) : eLpNorm f p Î¼ = eLpNorm g p Î½ := by
  by_cases h0 : p = 0
  Â· simp [h0]
  by_cases h_top : p = âˆž
  Â· simp only [h_top, eLpNorm, eLpNormEssSup, ENNReal.top_ne_zero, eq_self_iff_true, if_true,
      if_false]
    apply essSup_eq
    exact h.comp (measurable_coe_nnreal_ennreal.comp measurable_nnnorm)
  simp only [eLpNorm_eq_eLpNorm' h0 h_top, eLpNorm', one_div]
  congr 1
  apply lintegral_eq
  exact h.comp (Measurable.pow_const (measurable_coe_nnreal_ennreal.comp measurable_nnnorm)
    p.toReal)

theorem memLp_snd [NormedAddCommGroup Î³] [BorelSpace Î³] {p : â„â‰¥0âˆž} (h : IdentDistrib f g Î¼ Î½)
    (hf : MemLp f p Î¼) : MemLp g p Î½ := by
  refine âŸ¨h.aestronglyMeasurable_snd hf.aestronglyMeasurable, ?_âŸ©
  rw [â† h.eLpNorm_eq]
  exact hf.2

@[deprecated (since := "2025-02-21")]
alias memð“›p_snd := memLp_snd

theorem memLp_iff [NormedAddCommGroup Î³] [BorelSpace Î³] {p : â„â‰¥0âˆž} (h : IdentDistrib f g Î¼ Î½) :
    MemLp f p Î¼ â†” MemLp g p Î½ :=
  âŸ¨fun hf => h.memLp_snd hf, fun hg => h.symm.memLp_snd hgâŸ©

@[deprecated (since := "2025-02-21")]
alias memð“›p_iff := memLp_iff

theorem integrable_snd [NormedAddCommGroup Î³] [BorelSpace Î³] (h : IdentDistrib f g Î¼ Î½)
    (hf : Integrable f Î¼) : Integrable g Î½ := by
  rw [â† memLp_one_iff_integrable] at hf âŠ¢
  exact h.memLp_snd hf

theorem integrable_iff [NormedAddCommGroup Î³] [BorelSpace Î³] (h : IdentDistrib f g Î¼ Î½) :
    Integrable f Î¼ â†” Integrable g Î½ :=
  âŸ¨fun hf => h.integrable_snd hf, fun hg => h.symm.integrable_snd hgâŸ©

protected theorem norm [NormedAddCommGroup Î³] [BorelSpace Î³] (h : IdentDistrib f g Î¼ Î½) :
    IdentDistrib (fun x => â€–f xâ€–) (fun x => â€–g xâ€–) Î¼ Î½ :=
  h.comp measurable_norm

protected theorem nnnorm [NormedAddCommGroup Î³] [BorelSpace Î³] (h : IdentDistrib f g Î¼ Î½) :
    IdentDistrib (fun x => â€–f xâ€–â‚Š) (fun x => â€–g xâ€–â‚Š) Î¼ Î½ :=
  h.comp measurable_nnnorm

protected theorem pow [Pow Î³ â„•] [MeasurablePow Î³ â„•] (h : IdentDistrib f g Î¼ Î½) {n : â„•} :
    IdentDistrib (fun x => f x ^ n) (fun x => g x ^ n) Î¼ Î½ :=
  h.comp (measurable_id.pow_const n)

protected theorem sq [Pow Î³ â„•] [MeasurablePow Î³ â„•] (h : IdentDistrib f g Î¼ Î½) :
    IdentDistrib (fun x => f x ^ 2) (fun x => g x ^ 2) Î¼ Î½ :=
  h.comp (measurable_id.pow_const 2)

protected theorem coe_nnreal_ennreal {f : Î± â†’ â„â‰¥0} {g : Î² â†’ â„â‰¥0} (h : IdentDistrib f g Î¼ Î½) :
    IdentDistrib (fun x => (f x : â„â‰¥0âˆž)) (fun x => (g x : â„â‰¥0âˆž)) Î¼ Î½ :=
  h.comp measurable_coe_nnreal_ennreal

@[to_additive]
theorem mul_const [Mul Î³] [MeasurableMul Î³] (h : IdentDistrib f g Î¼ Î½) (c : Î³) :
    IdentDistrib (fun x => f x * c) (fun x => g x * c) Î¼ Î½ :=
  h.comp (measurable_mul_const c)

@[to_additive]
theorem const_mul [Mul Î³] [MeasurableMul Î³] (h : IdentDistrib f g Î¼ Î½) (c : Î³) :
    IdentDistrib (fun x => c * f x) (fun x => c * g x) Î¼ Î½ :=
  h.comp (measurable_const_mul c)

@[to_additive]
theorem div_const [Div Î³] [MeasurableDiv Î³] (h : IdentDistrib f g Î¼ Î½) (c : Î³) :
    IdentDistrib (fun x => f x / c) (fun x => g x / c) Î¼ Î½ :=
  h.comp (MeasurableDiv.measurable_div_const c)

@[to_additive]
theorem const_div [Div Î³] [MeasurableDiv Î³] (h : IdentDistrib f g Î¼ Î½) (c : Î³) :
    IdentDistrib (fun x => c / f x) (fun x => c / g x) Î¼ Î½ :=
  h.comp (MeasurableDiv.measurable_const_div c)

@[to_additive]
lemma inv [Inv Î³] [MeasurableInv Î³] (h : IdentDistrib f g Î¼ Î½) :
    IdentDistrib fâ»Â¹ gâ»Â¹ Î¼ Î½ := h.comp measurable_inv

theorem evariance_eq {f : Î± â†’ â„} {g : Î² â†’ â„} (h : IdentDistrib f g Î¼ Î½) :
    evariance f Î¼ = evariance g Î½ := by
  convert (h.sub_const (âˆ« x, f x âˆ‚Î¼)).nnnorm.coe_nnreal_ennreal.sq.lintegral_eq
  rw [h.integral_eq]
  rfl

theorem variance_eq {f : Î± â†’ â„} {g : Î² â†’ â„} (h : IdentDistrib f g Î¼ Î½) :
    variance f Î¼ = variance g Î½ := by rw [variance, h.evariance_eq]; rfl

end IdentDistrib

section UniformIntegrable

open TopologicalSpace

variable {E : Type*} [MeasurableSpace E] [NormedAddCommGroup E] [BorelSpace E]
  {Î¼ : Measure Î±} [IsFiniteMeasure Î¼]

/-- This lemma is superseded by `MemLp.uniformIntegrable_of_identDistrib` which only requires
`AEStronglyMeasurable`. -/
theorem MemLp.uniformIntegrable_of_identDistrib_aux {Î¹ : Type*} {f : Î¹ â†’ Î± â†’ E} {j : Î¹} {p : â„â‰¥0âˆž}
    (hp : 1 â‰¤ p) (hp' : p â‰  âˆž) (hâ„’p : MemLp (f j) p Î¼) (hfmeas : âˆ€ i, StronglyMeasurable (f i))
    (hf : âˆ€ i, IdentDistrib (f i) (f j) Î¼ Î¼) : UniformIntegrable f p Î¼ := by
  refine uniformIntegrable_of' hp hp' hfmeas fun Îµ hÎµ => ?_
  by_cases hÎ¹ : Nonempty Î¹
  swap; Â· exact âŸ¨0, fun i => False.elim (hÎ¹ <| Nonempty.intro i)âŸ©
  obtain âŸ¨C, hCâ‚, hCâ‚‚âŸ© := hâ„’p.eLpNorm_indicator_norm_ge_pos_le (hfmeas _) hÎµ
  refine âŸ¨âŸ¨C, hCâ‚.leâŸ©, fun i => le_trans (le_of_eq ?_) hCâ‚‚âŸ©
  have : {x | (âŸ¨C, hCâ‚.leâŸ© : â„â‰¥0) â‰¤ â€–f i xâ€–â‚Š} = {x | C â‰¤ â€–f i xâ€–} := by
    ext x
    simp_rw [â† norm_toNNReal]
    exact Real.le_toNNReal_iff_coe_le (norm_nonneg _)
  rw [this, â† eLpNorm_norm, â† eLpNorm_norm (Set.indicator _ _)]
  simp_rw [norm_indicator_eq_indicator_norm, coe_nnnorm]
  let F : E â†’ â„ := (fun x : E => if (âŸ¨C, hCâ‚.leâŸ© : â„â‰¥0) â‰¤ â€–xâ€–â‚Š then â€–xâ€– else 0)
  have F_meas : Measurable F := by
    apply measurable_norm.indicator (measurableSet_le measurable_const measurable_nnnorm)
  have : âˆ€ k, (fun x â†¦ Set.indicator {x | C â‰¤ â€–f k xâ€–} (fun a â†¦ â€–f k aâ€–) x) = F âˆ˜ f k := by
    intro k
    ext x
    simp only [Set.indicator, Set.mem_setOf_eq]; norm_cast
  rw [this, this, â† eLpNorm_map_measure F_meas.aestronglyMeasurable (hf i).aemeasurable_fst,
    (hf i).map_eq, eLpNorm_map_measure F_meas.aestronglyMeasurable (hf j).aemeasurable_fst]

@[deprecated (since := "2025-02-21")]
alias Memð“›p.uniformIntegrable_of_identDistrib_aux := MemLp.uniformIntegrable_of_identDistrib_aux

/-- A sequence of identically distributed Láµ– functions is p-uniformly integrable. -/
theorem MemLp.uniformIntegrable_of_identDistrib {Î¹ : Type*} {f : Î¹ â†’ Î± â†’ E} {j : Î¹} {p : â„â‰¥0âˆž}
    (hp : 1 â‰¤ p) (hp' : p â‰  âˆž) (hâ„’p : MemLp (f j) p Î¼) (hf : âˆ€ i, IdentDistrib (f i) (f j) Î¼ Î¼) :
    UniformIntegrable f p Î¼ := by
  have hfmeas : âˆ€ i, AEStronglyMeasurable (f i) Î¼ := fun i =>
    (hf i).aestronglyMeasurable_iff.2 hâ„’p.1
  set g : Î¹ â†’ Î± â†’ E := fun i => (hfmeas i).choose
  have hgmeas : âˆ€ i, StronglyMeasurable (g i) := fun i => (Exists.choose_spec <| hfmeas i).1
  have hgeq : âˆ€ i, g i =áµ[Î¼] f i := fun i => (Exists.choose_spec <| hfmeas i).2.symm
  have hgâ„’p : MemLp (g j) p Î¼ := hâ„’p.ae_eq (hgeq j).symm
  exact UniformIntegrable.ae_eq
    (MemLp.uniformIntegrable_of_identDistrib_aux hp hp' hgâ„’p hgmeas fun i =>
      (IdentDistrib.of_ae_eq (hgmeas i).aemeasurable (hgeq i)).trans
        ((hf i).trans <| IdentDistrib.of_ae_eq (hfmeas j).aemeasurable (hgeq j).symm)) hgeq

@[deprecated (since := "2025-02-21")]
alias Memð“›p.uniformIntegrable_of_identDistrib := MemLp.uniformIntegrable_of_identDistrib

end UniformIntegrable

/-- If `X` and `Y` are independent and `(X, Y)` and `(X', Y')` are identically distributed,
then `X'` and `Y'` are independent. -/
lemma indepFun_of_identDistrib_pair
    {Î¼ : Measure Î³} {Î¼' : Measure Î´} [IsFiniteMeasure Î¼] [IsFiniteMeasure Î¼']
    {X : Î³ â†’ Î±} {X' : Î´ â†’ Î±} {Y : Î³ â†’ Î²} {Y' : Î´ â†’ Î²} (h_indep : IndepFun X Y Î¼)
    (h_ident : IdentDistrib (fun Ï‰ â†¦ (X Ï‰, Y Ï‰)) (fun Ï‰ â†¦ (X' Ï‰, Y' Ï‰)) Î¼ Î¼') :
    IndepFun X' Y' Î¼' := by
  rw [indepFun_iff_map_prod_eq_prod_map_map _ _, â† h_ident.map_eq,
    (indepFun_iff_map_prod_eq_prod_map_map _ _).1 h_indep]
  Â· exact congr (congrArg Measure.prod <| (h_ident.comp measurable_fst).map_eq)
      (h_ident.comp measurable_snd).map_eq
  Â· exact measurable_fst.aemeasurable.comp_aemeasurable h_ident.aemeasurable_fst
  Â· exact measurable_snd.aemeasurable.comp_aemeasurable h_ident.aemeasurable_fst
  Â· exact measurable_fst.aemeasurable.comp_aemeasurable h_ident.aemeasurable_snd
  Â· exact measurable_snd.aemeasurable.comp_aemeasurable h_ident.aemeasurable_snd

end ProbabilityTheory
