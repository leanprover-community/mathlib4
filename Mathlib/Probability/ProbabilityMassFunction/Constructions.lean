/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Devon Tuma
-/
import Mathlib.Probability.ProbabilityMassFunction.Monad

#align_import probability.probability_mass_function.constructions from "leanprover-community/mathlib"@"4ac69b290818724c159de091daa3acd31da0ee6d"

/-!
# Specific Constructions of Probability Mass Functions

This file gives a number of different `Pmf` constructions for common probability distributions.

`map` and `seq` allow pushing a `Pmf Œ±` along a function `f : Œ± ‚Üí Œ≤` (or distribution of
functions `f : Pmf (Œ± ‚Üí Œ≤)`) to get a `Pmf Œ≤`.

`ofFinset` and `ofFintype` simplify the construction of a `Pmf Œ±` from a function `f : Œ± ‚Üí ‚Ñù‚â•0‚àû`,
by allowing the "sum equals 1" constraint to be in terms of `Finset.sum` instead of `tsum`.

`normalize` constructs a `Pmf Œ±` by normalizing a function `f : Œ± ‚Üí ‚Ñù‚â•0‚àû` by its sum,
and `filter` uses this to filter the support of a `Pmf` and re-normalize the new distribution.

`bernoulli` represents the bernoulli distribution on `Bool`.

-/


namespace Pmf

noncomputable section

variable {Œ± Œ≤ Œ≥ : Type*}

open Classical BigOperators NNReal ENNReal

section Map

/-- The functorial action of a function on a `Pmf`. -/
def map (f : Œ± ‚Üí Œ≤) (p : Pmf Œ±) : Pmf Œ≤ :=
  bind p (pure ‚àò f)
#align pmf.map Pmf.map

variable (f : Œ± ‚Üí Œ≤) (p : Pmf Œ±) (b : Œ≤)

theorem monad_map_eq_map {Œ± Œ≤ : Type _} (f : Œ± ‚Üí Œ≤) (p : Pmf Œ±) : f <$> p = p.map f := rfl
#align pmf.monad_map_eq_map Pmf.monad_map_eq_map

@[simp]
theorem map_apply : (map f p) b = ‚àë' a, if b = f a then p a else 0 := by simp [map]
                                                                         -- üéâ no goals
#align pmf.map_apply Pmf.map_apply

@[simp]
theorem support_map : (map f p).support = f '' p.support :=
  Set.ext fun b => by simp [map, @eq_comm Œ≤ b]
                      -- üéâ no goals
#align pmf.support_map Pmf.support_map

theorem mem_support_map_iff : b ‚àà (map f p).support ‚Üî ‚àÉ a ‚àà p.support, f a = b := by simp
                                                                                     -- üéâ no goals
#align pmf.mem_support_map_iff Pmf.mem_support_map_iff

theorem bind_pure_comp : bind p (pure ‚àò f) = map f p := rfl
#align pmf.bind_pure_comp Pmf.bind_pure_comp

theorem map_id : map id p = p :=
  bind_pure _
#align pmf.map_id Pmf.map_id

theorem map_comp (g : Œ≤ ‚Üí Œ≥) : (p.map f).map g = p.map (g ‚àò f) := by simp [map, Function.comp]
                                                                     -- üéâ no goals
#align pmf.map_comp Pmf.map_comp

theorem pure_map (a : Œ±) : (pure a).map f = pure (f a) :=
  pure_bind _ _
#align pmf.pure_map Pmf.pure_map

theorem map_bind (q : Œ± ‚Üí Pmf Œ≤) (f : Œ≤ ‚Üí Œ≥) : (p.bind q).map f = p.bind fun a => (q a).map f :=
  bind_bind _ _ _
#align pmf.map_bind Pmf.map_bind

@[simp]
theorem bind_map (p : Pmf Œ±) (f : Œ± ‚Üí Œ≤) (q : Œ≤ ‚Üí Pmf Œ≥) : (p.map f).bind q = p.bind (q ‚àò f) :=
  (bind_bind _ _ _).trans (congr_arg _ (funext fun _ => pure_bind _ _))
#align pmf.bind_map Pmf.bind_map

@[simp]
theorem map_const : p.map (Function.const Œ± b) = pure b := by
  simp only [map, Function.comp, bind_const, Function.const]
  -- üéâ no goals
#align pmf.map_const Pmf.map_const

section Measure

variable (s : Set Œ≤)

@[simp]
theorem toOuterMeasure_map_apply : (p.map f).toOuterMeasure s = p.toOuterMeasure (f ‚Åª¬π' s) := by
  simp [map, Set.indicator, toOuterMeasure_apply p (f ‚Åª¬π' s)]
  -- üéâ no goals
#align pmf.to_outer_measure_map_apply Pmf.toOuterMeasure_map_apply

@[simp]
theorem toMeasure_map_apply [MeasurableSpace Œ±] [MeasurableSpace Œ≤] (hf : Measurable f)
    (hs : MeasurableSet s) : (p.map f).toMeasure s = p.toMeasure (f ‚Åª¬π' s) := by
  rw [toMeasure_apply_eq_toOuterMeasure_apply _ s hs,
    toMeasure_apply_eq_toOuterMeasure_apply _ (f ‚Åª¬π' s) (measurableSet_preimage hf hs)]
  exact toOuterMeasure_map_apply f p s
  -- üéâ no goals
#align pmf.to_measure_map_apply Pmf.toMeasure_map_apply

end Measure

end Map

section Seq

/-- The monadic sequencing operation for `Pmf`. -/
def seq (q : Pmf (Œ± ‚Üí Œ≤)) (p : Pmf Œ±) : Pmf Œ≤ :=
  q.bind fun m => p.bind fun a => pure (m a)
#align pmf.seq Pmf.seq

variable (q : Pmf (Œ± ‚Üí Œ≤)) (p : Pmf Œ±) (b : Œ≤)

theorem monad_seq_eq_seq {Œ± Œ≤ : Type _} (q : Pmf (Œ± ‚Üí Œ≤)) (p : Pmf Œ±) : q <*> p = q.seq p := rfl
#align pmf.monad_seq_eq_seq Pmf.monad_seq_eq_seq

@[simp]
theorem seq_apply : (seq q p) b = ‚àë' (f : Œ± ‚Üí Œ≤) (a : Œ±), if b = f a then q f * p a else 0 := by
  simp only [seq, mul_boole, bind_apply, pure_apply]
  -- ‚ä¢ (‚àë' (a : Œ± ‚Üí Œ≤), ‚Üëq a * ‚àë' (a_1 : Œ±), if b = a a_1 then ‚Üëp a_1 else 0) = ‚àë'  ‚Ä¶
  refine' tsum_congr fun f => ENNReal.tsum_mul_left.symm.trans (tsum_congr fun a => _)
  -- ‚ä¢ (‚Üëq f * if b = f a then ‚Üëp a else 0) = if b = f a then ‚Üëq f * ‚Üëp a else 0
  simpa only [mul_zero] using mul_ite (b = f a) (q f) (p a) 0
  -- üéâ no goals
#align pmf.seq_apply Pmf.seq_apply

@[simp]
theorem support_seq : (seq q p).support = ‚ãÉ f ‚àà q.support, f '' p.support :=
  Set.ext fun b => by simp [-mem_support_iff, seq, @eq_comm Œ≤ b]
                      -- üéâ no goals
#align pmf.support_seq Pmf.support_seq

theorem mem_support_seq_iff : b ‚àà (seq q p).support ‚Üî ‚àÉ f ‚àà q.support, b ‚àà f '' p.support := by simp
                                                                                                -- üéâ no goals
#align pmf.mem_support_seq_iff Pmf.mem_support_seq_iff

end Seq

instance : LawfulFunctor Pmf where
  map_const := rfl
  id_map := bind_pure
  comp_map _ _ _ := (map_comp _ _ _).symm

instance : LawfulMonad Pmf := LawfulMonad.mk'
  (bind_pure_comp := fun f x => rfl)
  (id_map := id_map)
  (pure_bind := pure_bind)
  (bind_assoc := bind_bind)

section OfFinset

/-- Given a finset `s` and a function `f : Œ± ‚Üí ‚Ñù‚â•0‚àû` with sum `1` on `s`,
  such that `f a = 0` for `a ‚àâ s`, we get a `Pmf`. -/
def ofFinset (f : Œ± ‚Üí ‚Ñù‚â•0‚àû) (s : Finset Œ±) (h : ‚àë a in s, f a = 1)
    (h' : ‚àÄ (a) (_ : a ‚àâ s), f a = 0) : Pmf Œ± :=
  ‚ü®f, h ‚ñ∏ hasSum_sum_of_ne_finset_zero h'‚ü©
#align pmf.of_finset Pmf.ofFinset

variable {f : Œ± ‚Üí ‚Ñù‚â•0‚àû} {s : Finset Œ±} (h : ‚àë a in s, f a = 1) (h' : ‚àÄ (a) (_ : a ‚àâ s), f a = 0)

@[simp]
theorem ofFinset_apply (a : Œ±) : ofFinset f s h h' a = f a := rfl
#align pmf.of_finset_apply Pmf.ofFinset_apply

@[simp]
theorem support_ofFinset : (ofFinset f s h h').support = ‚Üës ‚à© Function.support f :=
  Set.ext fun a => by simpa [mem_support_iff] using mt (h' a)
                      -- üéâ no goals
#align pmf.support_of_finset Pmf.support_ofFinset

theorem mem_support_ofFinset_iff (a : Œ±) : a ‚àà (ofFinset f s h h').support ‚Üî a ‚àà s ‚àß f a ‚â† 0 := by
  simp
  -- üéâ no goals
#align pmf.mem_support_of_finset_iff Pmf.mem_support_ofFinset_iff

theorem ofFinset_apply_of_not_mem {a : Œ±} (ha : a ‚àâ s) : ofFinset f s h h' a = 0 :=
  h' a ha
#align pmf.of_finset_apply_of_not_mem Pmf.ofFinset_apply_of_not_mem

section Measure

variable (t : Set Œ±)

@[simp]
theorem toOuterMeasure_ofFinset_apply :
    (ofFinset f s h h').toOuterMeasure t = ‚àë' x, t.indicator f x :=
  toOuterMeasure_apply (ofFinset f s h h') t
#align pmf.to_outer_measure_of_finset_apply Pmf.toOuterMeasure_ofFinset_apply

@[simp]
theorem toMeasure_ofFinset_apply [MeasurableSpace Œ±] (ht : MeasurableSet t) :
    (ofFinset f s h h').toMeasure t = ‚àë' x, t.indicator f x :=
  (toMeasure_apply_eq_toOuterMeasure_apply _ t ht).trans (toOuterMeasure_ofFinset_apply h h' t)
#align pmf.to_measure_of_finset_apply Pmf.toMeasure_ofFinset_apply

end Measure

end OfFinset

section OfFintype

/-- Given a finite type `Œ±` and a function `f : Œ± ‚Üí ‚Ñù‚â•0‚àû` with sum 1, we get a `Pmf`. -/
def ofFintype [Fintype Œ±] (f : Œ± ‚Üí ‚Ñù‚â•0‚àû) (h : ‚àë a, f a = 1) : Pmf Œ± :=
  ofFinset f Finset.univ h fun a ha => absurd (Finset.mem_univ a) ha
#align pmf.of_fintype Pmf.ofFintype

variable [Fintype Œ±] {f : Œ± ‚Üí ‚Ñù‚â•0‚àû} (h : ‚àë a, f a = 1)

@[simp]
theorem ofFintype_apply (a : Œ±) : ofFintype f h a = f a := rfl
#align pmf.of_fintype_apply Pmf.ofFintype_apply

@[simp]
theorem support_ofFintype : (ofFintype f h).support = Function.support f := rfl
#align pmf.support_of_fintype Pmf.support_ofFintype

theorem mem_support_ofFintype_iff (a : Œ±) : a ‚àà (ofFintype f h).support ‚Üî f a ‚â† 0 := Iff.rfl
#align pmf.mem_support_of_fintype_iff Pmf.mem_support_ofFintype_iff

section Measure

variable (s : Set Œ±)

@[simp high]
theorem toOuterMeasure_ofFintype_apply : (ofFintype f h).toOuterMeasure s = ‚àë' x, s.indicator f x :=
  toOuterMeasure_apply (ofFintype f h) s
#align pmf.to_outer_measure_of_fintype_apply Pmf.toOuterMeasure_ofFintype_apply

@[simp]
theorem toMeasure_ofFintype_apply [MeasurableSpace Œ±] (hs : MeasurableSet s) :
    (ofFintype f h).toMeasure s = ‚àë' x, s.indicator f x :=
  (toMeasure_apply_eq_toOuterMeasure_apply _ s hs).trans (toOuterMeasure_ofFintype_apply h s)
#align pmf.to_measure_of_fintype_apply Pmf.toMeasure_ofFintype_apply

end Measure

end OfFintype

section normalize

/-- Given an `f` with non-zero and non-infinite sum, get a `Pmf` by normalizing `f` by its `tsum`.
-/
def normalize (f : Œ± ‚Üí ‚Ñù‚â•0‚àû) (hf0 : tsum f ‚â† 0) (hf : tsum f ‚â† ‚àû) : Pmf Œ± :=
  ‚ü®fun a => f a * (‚àë' x, f x)‚Åª¬π,
    ENNReal.summable.hasSum_iff.2 (ENNReal.tsum_mul_right.trans (ENNReal.mul_inv_cancel hf0 hf))‚ü©
#align pmf.normalize Pmf.normalize

variable {f : Œ± ‚Üí ‚Ñù‚â•0‚àû} (hf0 : tsum f ‚â† 0) (hf : tsum f ‚â† ‚àû)

@[simp]
theorem normalize_apply (a : Œ±) : (normalize f hf0 hf) a = f a * (‚àë' x, f x)‚Åª¬π := rfl
#align pmf.normalize_apply Pmf.normalize_apply

@[simp]
theorem support_normalize : (normalize f hf0 hf).support = Function.support f :=
  Set.ext fun a => by simp [hf, mem_support_iff]
                      -- üéâ no goals
#align pmf.support_normalize Pmf.support_normalize

theorem mem_support_normalize_iff (a : Œ±) : a ‚àà (normalize f hf0 hf).support ‚Üî f a ‚â† 0 := by simp
                                                                                             -- üéâ no goals
#align pmf.mem_support_normalize_iff Pmf.mem_support_normalize_iff

end normalize

section Filter

/-- Create new `Pmf` by filtering on a set with non-zero measure and normalizing. -/
def filter (p : Pmf Œ±) (s : Set Œ±) (h : ‚àÉ a ‚àà s, a ‚àà p.support) : Pmf Œ± :=
  Pmf.normalize (s.indicator p) (by simpa using h) (p.tsum_coe_indicator_ne_top s)
                                    -- üéâ no goals
#align pmf.filter Pmf.filter

variable {p : Pmf Œ±} {s : Set Œ±} (h : ‚àÉ a ‚àà s, a ‚àà p.support)

@[simp]
theorem filter_apply (a : Œ±) :
    (p.filter s h) a = s.indicator p a * (‚àë' a', (s.indicator p) a')‚Åª¬π := by
  rw [filter, normalize_apply]
  -- üéâ no goals
#align pmf.filter_apply Pmf.filter_apply

theorem filter_apply_eq_zero_of_not_mem {a : Œ±} (ha : a ‚àâ s) : (p.filter s h) a = 0 := by
  rw [filter_apply, Set.indicator_apply_eq_zero.mpr fun ha' => absurd ha' ha, zero_mul]
  -- üéâ no goals
#align pmf.filter_apply_eq_zero_of_not_mem Pmf.filter_apply_eq_zero_of_not_mem

theorem mem_support_filter_iff {a : Œ±} : a ‚àà (p.filter s h).support ‚Üî a ‚àà s ‚àß a ‚àà p.support :=
  (mem_support_normalize_iff _ _ _).trans Set.indicator_apply_ne_zero
#align pmf.mem_support_filter_iff Pmf.mem_support_filter_iff

@[simp]
theorem support_filter : (p.filter s h).support = s ‚à© p.support :=
  Set.ext fun _ => mem_support_filter_iff _
#align pmf.support_filter Pmf.support_filter

theorem filter_apply_eq_zero_iff (a : Œ±) : (p.filter s h) a = 0 ‚Üî a ‚àâ s ‚à® a ‚àâ p.support := by
  erw [apply_eq_zero_iff, support_filter, Set.mem_inter_iff, not_and_or]
  -- üéâ no goals
#align pmf.filter_apply_eq_zero_iff Pmf.filter_apply_eq_zero_iff

theorem filter_apply_ne_zero_iff (a : Œ±) : (p.filter s h) a ‚â† 0 ‚Üî a ‚àà s ‚àß a ‚àà p.support := by
  rw [Ne.def, filter_apply_eq_zero_iff, not_or, Classical.not_not, Classical.not_not]
  -- üéâ no goals
#align pmf.filter_apply_ne_zero_iff Pmf.filter_apply_ne_zero_iff

end Filter

section bernoulli

/-- A `Pmf` which assigns probability `p` to `true` and `1 - p` to `false`. -/
def bernoulli (p : ‚Ñù‚â•0‚àû) (h : p ‚â§ 1) : Pmf Bool :=
  ofFintype (fun b => cond b p (1 - p)) (by simp [h])
                                            -- üéâ no goals
#align pmf.bernoulli Pmf.bernoulli

variable {p : ‚Ñù‚â•0‚àû} (h : p ‚â§ 1) (b : Bool)

@[simp]
theorem bernoulli_apply : bernoulli p h b = cond b p (1 - p) := rfl
#align pmf.bernoulli_apply Pmf.bernoulli_apply

@[simp]
theorem support_bernoulli : (bernoulli p h).support = { b | cond b (p ‚â† 0) (p ‚â† 1) } := by
  refine' Set.ext fun b => _
  -- ‚ä¢ b ‚àà support (bernoulli p h) ‚Üî b ‚àà {b | bif b then p ‚â† 0 else p ‚â† 1}
  induction b
  -- ‚ä¢ false ‚àà support (bernoulli p h) ‚Üî false ‚àà {b | bif b then p ‚â† 0 else p ‚â† 1}
  ¬∑ simp_rw [mem_support_iff, bernoulli_apply, Bool.cond_false, Ne.def, tsub_eq_zero_iff_le, not_le]
    -- ‚ä¢ p < 1 ‚Üî false ‚àà {b | bif b then ¬¨p = 0 else ¬¨p = 1}
    exact ‚ü®ne_of_lt, lt_of_le_of_ne h‚ü©
    -- üéâ no goals
  ¬∑ simp only [mem_support_iff, bernoulli_apply, Bool.cond_true, Set.mem_setOf_eq]
    -- üéâ no goals
#align pmf.support_bernoulli Pmf.support_bernoulli

theorem mem_support_bernoulli_iff : b ‚àà (bernoulli p h).support ‚Üî cond b (p ‚â† 0) (p ‚â† 1) := by simp
                                                                                               -- üéâ no goals
#align pmf.mem_support_bernoulli_iff Pmf.mem_support_bernoulli_iff

end bernoulli

end

end Pmf
