/-
Copyright (c) 2022 Devon Tuma. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Devon Tuma
-/
import Mathlib.Probability.ProbabilityMassFunction.Constructions

#align_import probability.probability_mass_function.uniform from "leanprover-community/mathlib"@"e50b8c261b0a000b806ec0e1356b41945eda61f7"

/-!
# Uniform Probability Mass Functions

This file defines a number of uniform `Pmf` distributions from various inputs,
  uniformly drawing from the corresponding object.

`Pmf.uniformOfFinset` gives each element in the set equal probability,
  with `0` probability for elements not in the set.

`Pmf.uniformOfFintype` gives all elements equal probability,
  equal to the inverse of the size of the `Fintype`.

`Pmf.ofMultiset` draws randomly from the given `Multiset`, treating duplicate values as distinct.
  Each probability is given by the count of the element divided by the size of the `Multiset`

-/

noncomputable section

namespace Pmf

variable {Î± Î² Î³ : Type*}

open scoped Classical BigOperators NNReal ENNReal

section UniformOfFinset

/-- Uniform distribution taking the same non-zero probability on the nonempty finset `s` -/
def uniformOfFinset (s : Finset Î±) (hs : s.Nonempty) : Pmf Î± := by
  refine' ofFinset (fun a => if a âˆˆ s then s.cardâ»Â¹ else 0) s _ _
  -- âŠ¢ âˆ‘ a in s, (fun a => if a âˆˆ s then (â†‘(Finset.card s))â»Â¹ else 0) a = 1
  Â· simp only [Finset.sum_ite_mem, Finset.inter_self, Finset.sum_const, nsmul_eq_mul]
    -- âŠ¢ â†‘(Finset.card s) * (â†‘(Finset.card s))â»Â¹ = 1
    have : (s.card : â„â‰¥0âˆ) â‰  0 := by
      simpa only [Ne.def, Nat.cast_eq_zero, Finset.card_eq_zero] using
        Finset.nonempty_iff_ne_empty.1 hs
    refine' ENNReal.mul_inv_cancel this <| ENNReal.nat_ne_top s.card
    -- ğŸ‰ no goals
  Â· exact fun x hx => by simp only [hx, if_false]
    -- ğŸ‰ no goals
#align pmf.uniform_of_finset Pmf.uniformOfFinset

variable {s : Finset Î±} (hs : s.Nonempty) {a : Î±}

@[simp]
theorem uniformOfFinset_apply (a : Î±) :
    uniformOfFinset s hs a = if a âˆˆ s then (s.card : â„â‰¥0âˆ)â»Â¹ else 0 :=
  rfl
#align pmf.uniform_of_finset_apply Pmf.uniformOfFinset_apply

theorem uniformOfFinset_apply_of_mem (ha : a âˆˆ s) : uniformOfFinset s hs a = (s.card : â„â‰¥0âˆ)â»Â¹ := by
  simp [ha]
  -- ğŸ‰ no goals
#align pmf.uniform_of_finset_apply_of_mem Pmf.uniformOfFinset_apply_of_mem

theorem uniformOfFinset_apply_of_not_mem (ha : a âˆ‰ s) : uniformOfFinset s hs a = 0 := by simp [ha]
                                                                                         -- ğŸ‰ no goals
#align pmf.uniform_of_finset_apply_of_not_mem Pmf.uniformOfFinset_apply_of_not_mem

@[simp]
theorem support_uniformOfFinset : (uniformOfFinset s hs).support = s :=
  Set.ext
    (by
      let âŸ¨a, haâŸ© := hs
      -- âŠ¢ âˆ€ (x : Î±), x âˆˆ support (uniformOfFinset s (_ : âˆƒ x, x âˆˆ s)) â†” x âˆˆ â†‘s
      simp [mem_support_iff, Finset.ne_empty_of_mem ha])
      -- ğŸ‰ no goals
#align pmf.support_uniform_of_finset Pmf.support_uniformOfFinset

theorem mem_support_uniformOfFinset_iff (a : Î±) : a âˆˆ (uniformOfFinset s hs).support â†” a âˆˆ s := by
  simp
  -- ğŸ‰ no goals
#align pmf.mem_support_uniform_of_finset_iff Pmf.mem_support_uniformOfFinset_iff

section Measure

variable (t : Set Î±)

@[simp]
theorem toOuterMeasure_uniformOfFinset_apply :
    (uniformOfFinset s hs).toOuterMeasure t = (s.filter (Â· âˆˆ t)).card / s.card :=
  calc
    (uniformOfFinset s hs).toOuterMeasure t = âˆ‘' x, if x âˆˆ t then uniformOfFinset s hs x else 0 :=
      toOuterMeasure_apply (uniformOfFinset s hs) t
    _ = âˆ‘' x, if x âˆˆ s âˆ§ x âˆˆ t then (s.card : â„â‰¥0âˆ)â»Â¹ else 0 :=
      (tsum_congr fun x => by simp_rw [uniformOfFinset_apply, â† ite_and, and_comm])
                              -- ğŸ‰ no goals
    _ = âˆ‘ x in s.filter (Â· âˆˆ t), if x âˆˆ s âˆ§ x âˆˆ t then (s.card : â„â‰¥0âˆ)â»Â¹ else 0 :=
      (tsum_eq_sum fun x hx => if_neg fun h => hx (Finset.mem_filter.2 h))
    _ = âˆ‘ _x in s.filter (Â· âˆˆ t), (s.card : â„â‰¥0âˆ)â»Â¹ :=
      (Finset.sum_congr rfl fun x hx => by
        let this : x âˆˆ s âˆ§ x âˆˆ t := by simpa using hx
        -- âŠ¢ (if x âˆˆ s âˆ§ x âˆˆ t then (â†‘(Finset.card s))â»Â¹ else 0) = (â†‘(Finset.card s))â»Â¹
        simp only [this, and_self_iff, if_true])
        -- ğŸ‰ no goals
    _ = (s.filter (Â· âˆˆ t)).card / s.card := by
        simp only [div_eq_mul_inv, Finset.sum_const, nsmul_eq_mul]
        -- ğŸ‰ no goals

#align pmf.to_outer_measure_uniform_of_finset_apply Pmf.toOuterMeasure_uniformOfFinset_apply

@[simp]
theorem toMeasure_uniformOfFinset_apply [MeasurableSpace Î±] (ht : MeasurableSet t) :
    (uniformOfFinset s hs).toMeasure t = (s.filter (Â· âˆˆ t)).card / s.card :=
  (toMeasure_apply_eq_toOuterMeasure_apply _ t ht).trans (toOuterMeasure_uniformOfFinset_apply hs t)
#align pmf.to_measure_uniform_of_finset_apply Pmf.toMeasure_uniformOfFinset_apply

end Measure

end UniformOfFinset

section UniformOfFintype

/-- The uniform pmf taking the same uniform value on all of the fintype `Î±` -/
def uniformOfFintype (Î± : Type*) [Fintype Î±] [Nonempty Î±] : Pmf Î± :=
  uniformOfFinset Finset.univ Finset.univ_nonempty
#align pmf.uniform_of_fintype Pmf.uniformOfFintype

variable [Fintype Î±] [Nonempty Î±]

@[simp]
theorem uniformOfFintype_apply (a : Î±) : uniformOfFintype Î± a = (Fintype.card Î± : â„â‰¥0âˆ)â»Â¹ := by
  simp [uniformOfFintype, Finset.mem_univ, if_true, uniformOfFinset_apply]
  -- âŠ¢ Finset.card Finset.univ = Fintype.card Î±
  rfl
  -- ğŸ‰ no goals
#align pmf.uniform_of_fintype_apply Pmf.uniformOfFintype_apply

@[simp]
theorem support_uniformOfFintype (Î± : Type*) [Fintype Î±] [Nonempty Î±] :
    (uniformOfFintype Î±).support = âŠ¤ :=
  Set.ext fun x => by simp [mem_support_iff]
                      -- ğŸ‰ no goals
#align pmf.support_uniform_of_fintype Pmf.support_uniformOfFintype

theorem mem_support_uniformOfFintype (a : Î±) : a âˆˆ (uniformOfFintype Î±).support := by simp
                                                                                      -- ğŸ‰ no goals
#align pmf.mem_support_uniform_of_fintype Pmf.mem_support_uniformOfFintype

section Measure

variable (s : Set Î±)

theorem toOuterMeasure_uniformOfFintype_apply :
    (uniformOfFintype Î±).toOuterMeasure s = Fintype.card s / Fintype.card Î± := by
  rw [uniformOfFintype, toOuterMeasure_uniformOfFinset_apply,Fintype.card_ofFinset]
  -- âŠ¢ â†‘(Finset.card (Finset.filter (fun x => x âˆˆ s) Finset.univ)) / â†‘(Finset.card  â€¦
  rfl
  -- ğŸ‰ no goals
#align pmf.to_outer_measure_uniform_of_fintype_apply Pmf.toOuterMeasure_uniformOfFintype_apply

theorem toMeasure_uniformOfFintype_apply [MeasurableSpace Î±] (hs : MeasurableSet s) :
    (uniformOfFintype Î±).toMeasure s = Fintype.card s / Fintype.card Î± := by
  simp [uniformOfFintype, hs]
  -- âŠ¢ â†‘(Finset.card (Finset.filter (fun x => x âˆˆ s) Finset.univ)) / â†‘(Finset.card  â€¦
  rfl
  -- ğŸ‰ no goals
#align pmf.to_measure_uniform_of_fintype_apply Pmf.toMeasure_uniformOfFintype_apply

end Measure

end UniformOfFintype

section OfMultiset

/-- Given a non-empty multiset `s` we construct the `Pmf` which sends `a` to the fraction of
  elements in `s` that are `a`. -/
def ofMultiset (s : Multiset Î±) (hs : s â‰  0) : Pmf Î± :=
  âŸ¨fun a => s.count a / (Multiset.card s),
    ENNReal.summable.hasSum_iff.2
      (calc
        (âˆ‘' b : Î±, (s.count b : â„â‰¥0âˆ) / (Multiset.card s))
          = (Multiset.card s : â„â‰¥0âˆ)â»Â¹ * âˆ‘' b, (s.count b : â„â‰¥0âˆ) := by
            simp_rw [ENNReal.div_eq_inv_mul, ENNReal.tsum_mul_left]
            -- ğŸ‰ no goals
        _ = (Multiset.card s : â„â‰¥0âˆ)â»Â¹ * âˆ‘ b in s.toFinset, (s.count b : â„â‰¥0âˆ) :=
          (congr_arg (fun x => (Multiset.card s : â„â‰¥0âˆ)â»Â¹ * x)
            (tsum_eq_sum fun a ha =>
              Nat.cast_eq_zero.2 <| by rwa [Multiset.count_eq_zero, â† Multiset.mem_toFinset]))
                                       -- ğŸ‰ no goals
        _ = 1 := by
          rw [â† Nat.cast_sum, Multiset.toFinset_sum_count_eq s,
            ENNReal.inv_mul_cancel (Nat.cast_ne_zero.2 (hs âˆ˜ Multiset.card_eq_zero.1))
              (ENNReal.nat_ne_top _)]
        )âŸ©
#align pmf.of_multiset Pmf.ofMultiset

variable {s : Multiset Î±} (hs : s â‰  0)

@[simp]
theorem ofMultiset_apply (a : Î±) : ofMultiset s hs a = s.count a / (Multiset.card s) :=
  rfl
#align pmf.of_multiset_apply Pmf.ofMultiset_apply

@[simp]
theorem support_ofMultiset : (ofMultiset s hs).support = s.toFinset :=
  Set.ext (by simp [mem_support_iff, hs])
              -- ğŸ‰ no goals
#align pmf.support_of_multiset Pmf.support_ofMultiset

theorem mem_support_ofMultiset_iff (a : Î±) : a âˆˆ (ofMultiset s hs).support â†” a âˆˆ s.toFinset := by
  simp
  -- ğŸ‰ no goals
#align pmf.mem_support_of_multiset_iff Pmf.mem_support_ofMultiset_iff

theorem ofMultiset_apply_of_not_mem {a : Î±} (ha : a âˆ‰ s) : ofMultiset s hs a = 0 := by
  simpa only [ofMultiset_apply, ENNReal.div_eq_zero_iff, Nat.cast_eq_zero, Multiset.count_eq_zero,
    ENNReal.nat_ne_top, or_false_iff] using ha
#align pmf.of_multiset_apply_of_not_mem Pmf.ofMultiset_apply_of_not_mem

section Measure

variable (t : Set Î±)

@[simp]
theorem toOuterMeasure_ofMultiset_apply :
    (ofMultiset s hs).toOuterMeasure t =
      (âˆ‘' x, (s.filter (Â· âˆˆ t)).count x : â„â‰¥0âˆ) / (Multiset.card s) := by
  simp_rw [div_eq_mul_inv, â† ENNReal.tsum_mul_right, toOuterMeasure_apply]
  -- âŠ¢ âˆ‘' (x : Î±), Set.indicator t (â†‘(ofMultiset s hs)) x = âˆ‘' (i : Î±), â†‘(Multiset. â€¦
  refine' tsum_congr fun x => _
  -- âŠ¢ Set.indicator t (â†‘(ofMultiset s hs)) x = â†‘(Multiset.count x (Multiset.filter â€¦
  by_cases hx : x âˆˆ t <;> simp [Set.indicator, hx, div_eq_mul_inv]
  -- âŠ¢ Set.indicator t (â†‘(ofMultiset s hs)) x = â†‘(Multiset.count x (Multiset.filter â€¦
                          -- ğŸ‰ no goals
                          -- ğŸ‰ no goals
#align pmf.to_outer_measure_of_multiset_apply Pmf.toOuterMeasure_ofMultiset_apply

@[simp]
theorem toMeasure_ofMultiset_apply [MeasurableSpace Î±] (ht : MeasurableSet t) :
    (ofMultiset s hs).toMeasure t = (âˆ‘' x, (s.filter (Â· âˆˆ t)).count x : â„â‰¥0âˆ) / (Multiset.card s) :=
  (toMeasure_apply_eq_toOuterMeasure_apply _ t ht).trans (toOuterMeasure_ofMultiset_apply hs t)
#align pmf.to_measure_of_multiset_apply Pmf.toMeasure_ofMultiset_apply

end Measure

end OfMultiset

end Pmf
