/-
Copyright (c) 2021 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import Mathlib.MeasureTheory.Decomposition.RadonNikodym
import Mathlib.MeasureTheory.Measure.Haar.OfBasis
import Mathlib.Probability.Independence.Basic

#align_import probability.density from "leanprover-community/mathlib"@"c14c8fcde993801fca8946b0d80131a1a81d1520"

/-!
# Probability density function

This file defines the probability density function of random variables, by which we mean
measurable functions taking values in a Borel space. In particular, a measurable function `f`
is said to the probability density function of a random variable `X` if for all measurable
sets `S`, `‚Ñô(X ‚àà S) = ‚à´ x in S, f x dx`. Probability density functions are one way of describing
the distribution of a random variable, and are useful for calculating probabilities and
finding moments (although the latter is better achieved with moment generating functions).

This file also defines the continuous uniform distribution and proves some properties about
random variables with this distribution.

## Main definitions

* `MeasureTheory.HasPDF` : A random variable `X : Œ© ‚Üí E` is said to `HasPDF` with
  respect to the measure `‚Ñô` on `Œ©` and `Œº` on `E` if there exists a measurable function `f`
  such that the push-forward measure of `‚Ñô` along `X` equals `Œº.withDensity f`.
* `MeasureTheory.pdf` : If `X` is a random variable that `HasPDF X ‚Ñô Œº`, then `pdf X`
  is the measurable function `f` such that the push-forward measure of `‚Ñô` along `X` equals
  `Œº.withDensity f`.
* `MeasureTheory.pdf.IsUniform` : A random variable `X` is said to follow the uniform
  distribution if it has a constant probability density function with a compact, non-null support.

## Main results

* `MeasureTheory.pdf.integral_fun_mul_eq_integral` : Law of the unconscious statistician,
  i.e. if a random variable `X : Œ© ‚Üí E` has pdf `f`, then `ùîº(g(X)) = ‚à´ x, g x * f x dx` for
  all measurable `g : E ‚Üí ‚Ñù`.
* `MeasureTheory.pdf.integral_mul_eq_integral` : A real-valued random variable `X` with
  pdf `f` has expectation `‚à´ x, x * f x dx`.
* `MeasureTheory.pdf.IsUniform.integral_eq` : If `X` follows the uniform distribution with
  its pdf having support `s`, then `X` has expectation `(Œª s)‚Åª¬π * ‚à´ x in s, x dx` where `Œª`
  is the Lebesgue measure.

## TODOs

Ultimately, we would also like to define characteristic functions to describe distributions as
it exists for all random variables. However, to define this, we will need Fourier transforms
which we currently do not have.
-/


open scoped Classical MeasureTheory NNReal ENNReal

open TopologicalSpace MeasureTheory.Measure

noncomputable section

namespace MeasureTheory

variable {Œ© E : Type*} [MeasurableSpace E]

/-- A random variable `X : Œ© ‚Üí E` is said to `HasPDF` with respect to the measure `‚Ñô` on `Œ©` and
`Œº` on `E` if there exists a measurable function `f` such that the push-forward measure of `‚Ñô`
along `X` equals `Œº.withDensity f`. -/
class HasPDF {m : MeasurableSpace Œ©} (X : Œ© ‚Üí E) (‚Ñô : Measure Œ©)
    (Œº : Measure E := by volume_tac) : Prop where
  -- porting note: TODO: split into fields `Measurable` and `exists_pdf`
  pdf' : Measurable X ‚àß ‚àÉ f : E ‚Üí ‚Ñù‚â•0‚àû, Measurable f ‚àß map X ‚Ñô = Œº.withDensity f
#align measure_theory.has_pdf MeasureTheory.HasPDF

@[measurability]
theorem HasPDF.measurable {_ : MeasurableSpace Œ©} (X : Œ© ‚Üí E) (‚Ñô : Measure Œ©)
    (Œº : Measure E := by volume_tac) [hX : HasPDF X ‚Ñô Œº] :
    Measurable X :=
  hX.pdf'.1
#align measure_theory.has_pdf.measurable MeasureTheory.HasPDF.measurable

/-- If `X` is a random variable that `HasPDF X ‚Ñô Œº`, then `pdf X` is the measurable function `f`
such that the push-forward measure of `‚Ñô` along `X` equals `Œº.withDensity f`. -/
def pdf {_ : MeasurableSpace Œ©} (X : Œ© ‚Üí E) (‚Ñô : Measure Œ©) (Œº : Measure E := by volume_tac) :
    E ‚Üí ‚Ñù‚â•0‚àû :=
  if hX : HasPDF X ‚Ñô Œº then Classical.choose hX.pdf'.2 else 0
#align measure_theory.pdf MeasureTheory.pdf

theorem pdf_undef {m : MeasurableSpace Œ©} {‚Ñô : Measure Œ©} {Œº : Measure E} {X : Œ© ‚Üí E}
    (h : ¬¨HasPDF X ‚Ñô Œº) : pdf X ‚Ñô Œº = 0 := by simp only [pdf, dif_neg h]
#align measure_theory.pdf_undef MeasureTheory.pdf_undef

theorem hasPDF_of_pdf_ne_zero {m : MeasurableSpace Œ©} {‚Ñô : Measure Œ©} {Œº : Measure E} {X : Œ© ‚Üí E}
    (h : pdf X ‚Ñô Œº ‚â† 0) : HasPDF X ‚Ñô Œº := by
  by_contra hpdf
  simp [pdf, hpdf] at h
#align measure_theory.has_pdf_of_pdf_ne_zero MeasureTheory.hasPDF_of_pdf_ne_zero

theorem pdf_eq_zero_of_not_measurable {_ : MeasurableSpace Œ©} {‚Ñô : Measure Œ©} {Œº : Measure E}
    {X : Œ© ‚Üí E} (hX : ¬¨Measurable X) : pdf X ‚Ñô Œº = 0 :=
  pdf_undef fun hpdf => hX hpdf.pdf'.1
#align measure_theory.pdf_eq_zero_of_not_measurable MeasureTheory.pdf_eq_zero_of_not_measurable

theorem measurable_of_pdf_ne_zero {m : MeasurableSpace Œ©} {‚Ñô : Measure Œ©} {Œº : Measure E}
    (X : Œ© ‚Üí E) (h : pdf X ‚Ñô Œº ‚â† 0) : Measurable X := by
  by_contra hX
  exact h (pdf_eq_zero_of_not_measurable hX)
#align measure_theory.measurable_of_pdf_ne_zero MeasureTheory.measurable_of_pdf_ne_zero

@[measurability]
theorem measurable_pdf {m : MeasurableSpace Œ©} (X : Œ© ‚Üí E) (‚Ñô : Measure Œ©)
    (Œº : Measure E := by volume_tac) : Measurable (pdf X ‚Ñô Œº) := by
  unfold pdf
  split_ifs with h
  exacts [(Classical.choose_spec h.1.2).1, measurable_zero]
#align measure_theory.measurable_pdf MeasureTheory.measurable_pdf

theorem map_eq_withDensity_pdf {m : MeasurableSpace Œ©} (X : Œ© ‚Üí E) (‚Ñô : Measure Œ©)
    (Œº : Measure E := by volume_tac) [hX : HasPDF X ‚Ñô Œº] :
    Measure.map X ‚Ñô = Œº.withDensity (pdf X ‚Ñô Œº) := by
  simp only [pdf, dif_pos hX]
  exact (Classical.choose_spec hX.pdf'.2).2
#align measure_theory.map_eq_with_density_pdf MeasureTheory.map_eq_withDensity_pdf

theorem map_eq_set_lintegral_pdf {m : MeasurableSpace Œ©} (X : Œ© ‚Üí E) (‚Ñô : Measure Œ©)
    (Œº : Measure E := by volume_tac) [hX : HasPDF X ‚Ñô Œº] {s : Set E}
    (hs : MeasurableSet s) : Measure.map X ‚Ñô s = ‚à´‚Åª x in s, pdf X ‚Ñô Œº x ‚àÇŒº := by
  rw [‚Üê withDensity_apply _ hs, map_eq_withDensity_pdf X ‚Ñô Œº]
#align measure_theory.map_eq_set_lintegral_pdf MeasureTheory.map_eq_set_lintegral_pdf

namespace pdf

variable {m : MeasurableSpace Œ©} {‚Ñô : Measure Œ©} {Œº : Measure E}

theorem lintegral_eq_measure_univ {X : Œ© ‚Üí E} [HasPDF X ‚Ñô Œº] :
    ‚à´‚Åª x, pdf X ‚Ñô Œº x ‚àÇŒº = ‚Ñô Set.univ := by
  rw [‚Üê set_lintegral_univ, ‚Üê map_eq_set_lintegral_pdf X ‚Ñô Œº MeasurableSet.univ,
    Measure.map_apply (HasPDF.measurable X ‚Ñô Œº) MeasurableSet.univ, Set.preimage_univ]
#align measure_theory.pdf.lintegral_eq_measure_univ MeasureTheory.pdf.lintegral_eq_measure_univ

theorem unique [IsFiniteMeasure ‚Ñô] {X : Œ© ‚Üí E} [HasPDF X ‚Ñô Œº] (f : E ‚Üí ‚Ñù‚â•0‚àû)
    (hmf : AEMeasurable f Œº) : ‚Ñô.map X = Œº.withDensity f ‚Üî pdf X ‚Ñô Œº =·µê[Œº] f := by
  rw [map_eq_withDensity_pdf X ‚Ñô Œº]
  apply withDensity_eq_iff (measurable_pdf X ‚Ñô Œº).aemeasurable hmf
  rw [lintegral_eq_measure_univ]
  exact measure_ne_top _ _

theorem unique' [SigmaFinite Œº] {X : Œ© ‚Üí E} [HasPDF X ‚Ñô Œº] (f : E ‚Üí ‚Ñù‚â•0‚àû) (hmf : AEMeasurable f Œº) :
    ‚Ñô.map X = Œº.withDensity f ‚Üî pdf X ‚Ñô Œº =·µê[Œº] f :=
  map_eq_withDensity_pdf X ‚Ñô Œº ‚ñ∏
    withDensity_eq_iff_of_sigmaFinite (measurable_pdf X ‚Ñô Œº).aemeasurable hmf

nonrec theorem ae_lt_top [IsFiniteMeasure ‚Ñô] {Œº : Measure E} {X : Œ© ‚Üí E} :
    ‚àÄ·µê x ‚àÇŒº, pdf X ‚Ñô Œº x < ‚àû := by
  by_cases hpdf : HasPDF X ‚Ñô Œº
  ¬∑ haveI := hpdf
    refine' ae_lt_top (measurable_pdf X ‚Ñô Œº) _
    rw [lintegral_eq_measure_univ]
    exact measure_ne_top _ _
  ¬∑ simp [pdf, hpdf]
#align measure_theory.pdf.ae_lt_top MeasureTheory.pdf.ae_lt_top

nonrec theorem ofReal_toReal_ae_eq [IsFiniteMeasure ‚Ñô] {X : Œ© ‚Üí E} :
    (fun x => ENNReal.ofReal (pdf X ‚Ñô Œº x).toReal) =·µê[Œº] pdf X ‚Ñô Œº :=
  ofReal_toReal_ae_eq ae_lt_top
#align measure_theory.pdf.of_real_to_real_ae_eq MeasureTheory.pdf.ofReal_toReal_ae_eq

theorem integrable_iff_integrable_mul_pdf [IsFiniteMeasure ‚Ñô] {X : Œ© ‚Üí E} [HasPDF X ‚Ñô Œº] {f : E ‚Üí ‚Ñù}
    (hf : Measurable f) :
    Integrable (fun x => f (X x)) ‚Ñô ‚Üî Integrable (fun x => f x * (pdf X ‚Ñô Œº x).toReal) Œº := by
  -- porting note: using `erw` because `rw` doesn't recognize `(f <| X ¬∑)` as `f ‚àò X`
  -- https://github.com/leanprover-community/mathlib4/issues/5164
  erw [‚Üê integrable_map_measure hf.aestronglyMeasurable (HasPDF.measurable X ‚Ñô Œº).aemeasurable,
    map_eq_withDensity_pdf X ‚Ñô Œº, integrable_withDensity_iff (measurable_pdf _ _ _) ae_lt_top]
#align measure_theory.pdf.integrable_iff_integrable_mul_pdf MeasureTheory.pdf.integrable_iff_integrable_mul_pdf

/-- **The Law of the Unconscious Statistician**: Given a random variable `X` and a measurable
function `f`, `f ‚àò X` is a random variable with expectation `‚à´ x, f x * pdf X ‚àÇŒº`
where `Œº` is a measure on the codomain of `X`. -/
theorem integral_fun_mul_eq_integral [IsFiniteMeasure ‚Ñô] {X : Œ© ‚Üí E} [HasPDF X ‚Ñô Œº] {f : E ‚Üí ‚Ñù}
    (hf : Measurable f) : ‚à´ x, f x * (pdf X ‚Ñô Œº x).toReal ‚àÇŒº = ‚à´ x, f (X x) ‚àÇ‚Ñô := by
  by_cases hpdf : Integrable (fun x => f x * (pdf X ‚Ñô Œº x).toReal) Œº
  ¬∑ rw [‚Üê integral_map (HasPDF.measurable X ‚Ñô Œº).aemeasurable hf.aestronglyMeasurable,
      map_eq_withDensity_pdf X ‚Ñô Œº, integral_eq_lintegral_pos_part_sub_lintegral_neg_part hpdf,
      integral_eq_lintegral_pos_part_sub_lintegral_neg_part,
      lintegral_withDensity_eq_lintegral_mul _ (measurable_pdf X ‚Ñô Œº) hf.neg.ennreal_ofReal,
      lintegral_withDensity_eq_lintegral_mul _ (measurable_pdf X ‚Ñô Œº) hf.ennreal_ofReal]
    ¬∑ congr 2
      ¬∑ have : ‚àÄ x, ENNReal.ofReal (f x * (pdf X ‚Ñô Œº x).toReal) =
            ENNReal.ofReal (pdf X ‚Ñô Œº x).toReal * ENNReal.ofReal (f x) := fun x ‚Ü¶ by
          rw [mul_comm, ENNReal.ofReal_mul ENNReal.toReal_nonneg]
        simp_rw [this]
        exact lintegral_congr_ae (Filter.EventuallyEq.mul ofReal_toReal_ae_eq (ae_eq_refl _))
      ¬∑ have :
          ‚àÄ x,
            ENNReal.ofReal (-(f x * (pdf X ‚Ñô Œº x).toReal)) =
              ENNReal.ofReal (pdf X ‚Ñô Œº x).toReal * ENNReal.ofReal (-f x) := by
          intro x
          rw [neg_mul_eq_neg_mul, mul_comm, ENNReal.ofReal_mul ENNReal.toReal_nonneg]
        simp_rw [this]
        exact lintegral_congr_ae (Filter.EventuallyEq.mul ofReal_toReal_ae_eq (ae_eq_refl _))
    ¬∑ refine' ‚ü®hf.aestronglyMeasurable, _‚ü©
      rw [HasFiniteIntegral,
        lintegral_withDensity_eq_lintegral_mul _ (measurable_pdf _ _ _)
          hf.nnnorm.coe_nnreal_ennreal]
      have : (fun x => (pdf X ‚Ñô Œº * fun x => (‚Äñf x‚Äñ‚Çä : ‚Ñù‚â•0‚àû)) x) =·µê[Œº]
          fun x => ‚Äñf x * (pdf X ‚Ñô Œº x).toReal‚Äñ‚Çä := by
        simp_rw [‚Üê smul_eq_mul, nnnorm_smul, ENNReal.coe_mul]
        rw [smul_eq_mul, mul_comm]
        refine' Filter.EventuallyEq.mul (ae_eq_refl _) (ae_eq_trans ofReal_toReal_ae_eq.symm _)
        simp only [Real.ennnorm_eq_ofReal ENNReal.toReal_nonneg, ae_eq_refl]
      rw [lintegral_congr_ae this]
      exact hpdf.2
  ¬∑ rw [integral_undef hpdf, integral_undef]
    rwa [‚Üê integrable_iff_integrable_mul_pdf hf] at hpdf
#align measure_theory.pdf.integral_fun_mul_eq_integral MeasureTheory.pdf.integral_fun_mul_eq_integral

theorem map_absolutelyContinuous {X : Œ© ‚Üí E} [HasPDF X ‚Ñô Œº] : map X ‚Ñô ‚â™ Œº := by
  rw [map_eq_withDensity_pdf X ‚Ñô Œº]; exact withDensity_absolutelyContinuous _ _
#align measure_theory.pdf.map_absolutely_continuous MeasureTheory.pdf.map_absolutelyContinuous

/-- A random variable that `HasPDF` is quasi-measure preserving. -/
theorem to_quasiMeasurePreserving {X : Œ© ‚Üí E} [HasPDF X ‚Ñô Œº] : QuasiMeasurePreserving X ‚Ñô Œº :=
  { measurable := HasPDF.measurable X ‚Ñô Œº
    absolutelyContinuous := map_absolutelyContinuous }
#align measure_theory.pdf.to_quasi_measure_preserving MeasureTheory.pdf.to_quasiMeasurePreserving

theorem haveLebesgueDecomposition_of_hasPDF {X : Œ© ‚Üí E} [hX' : HasPDF X ‚Ñô Œº] :
    (map X ‚Ñô).HaveLebesgueDecomposition Œº :=
  ‚ü®‚ü®‚ü®0, pdf X ‚Ñô Œº‚ü©, by
      simp only [zero_add, measurable_pdf X ‚Ñô Œº, true_and_iff, MutuallySingular.zero_left,
        map_eq_withDensity_pdf X ‚Ñô Œº]‚ü©‚ü©
#align measure_theory.pdf.have_lebesgue_decomposition_of_has_pdf MeasureTheory.pdf.haveLebesgueDecomposition_of_hasPDF

theorem hasPDF_iff {X : Œ© ‚Üí E} :
    HasPDF X ‚Ñô Œº ‚Üî Measurable X ‚àß (map X ‚Ñô).HaveLebesgueDecomposition Œº ‚àß map X ‚Ñô ‚â™ Œº := by
  constructor
  ¬∑ intro hX'
    exact ‚ü®hX'.pdf'.1, haveLebesgueDecomposition_of_hasPDF, map_absolutelyContinuous‚ü©
  ¬∑ rintro ‚ü®hX, h_decomp, h‚ü©
    haveI := h_decomp
    refine' ‚ü®‚ü®hX, (Measure.map X ‚Ñô).rnDeriv Œº, measurable_rnDeriv _ _, _‚ü©‚ü©
    rwa [withDensity_rnDeriv_eq]
#align measure_theory.pdf.has_pdf_iff MeasureTheory.pdf.hasPDF_iff

theorem hasPDF_iff_of_measurable {X : Œ© ‚Üí E} (hX : Measurable X) :
    HasPDF X ‚Ñô Œº ‚Üî (map X ‚Ñô).HaveLebesgueDecomposition Œº ‚àß map X ‚Ñô ‚â™ Œº := by
  rw [hasPDF_iff]
  simp only [hX, true_and]
#align measure_theory.pdf.has_pdf_iff_of_measurable MeasureTheory.pdf.hasPDF_iff_of_measurable

section

variable {F : Type*} [MeasurableSpace F] {ŒΩ : Measure F}

/-- A random variable that `HasPDF` transformed under a `QuasiMeasurePreserving`
map also `HasPDF` if `(map g (map X ‚Ñô)).HaveLebesgueDecomposition Œº`.

`quasiMeasurePreserving_hasPDF` is more useful in the case we are working with a
probability measure and a real-valued random variable. -/
theorem quasiMeasurePreserving_hasPDF {X : Œ© ‚Üí E} [HasPDF X ‚Ñô Œº] {g : E ‚Üí F}
    (hg : QuasiMeasurePreserving g Œº ŒΩ) (hmap : (map g (map X ‚Ñô)).HaveLebesgueDecomposition ŒΩ) :
    HasPDF (g ‚àò X) ‚Ñô ŒΩ := by
  rw [hasPDF_iff, ‚Üê map_map hg.measurable (HasPDF.measurable X ‚Ñô Œº)]
  refine' ‚ü®hg.measurable.comp (HasPDF.measurable X ‚Ñô Œº), hmap, _‚ü©
  rw [map_eq_withDensity_pdf X ‚Ñô Œº]
  refine' AbsolutelyContinuous.mk fun s hsm hs => _
  rw [map_apply hg.measurable hsm, withDensity_apply _ (hg.measurable hsm)]
  have := hg.absolutelyContinuous hs
  rw [map_apply hg.measurable hsm] at this
  exact set_lintegral_measure_zero _ _ this
#align measure_theory.pdf.quasi_measure_preserving_has_pdf MeasureTheory.pdf.quasiMeasurePreserving_hasPDF

theorem quasiMeasurePreserving_hasPDF' [IsFiniteMeasure ‚Ñô] [SigmaFinite ŒΩ] {X : Œ© ‚Üí E}
    [HasPDF X ‚Ñô Œº] {g : E ‚Üí F} (hg : QuasiMeasurePreserving g Œº ŒΩ) : HasPDF (g ‚àò X) ‚Ñô ŒΩ :=
  quasiMeasurePreserving_hasPDF hg inferInstance
#align measure_theory.pdf.quasi_measure_preserving_has_pdf' MeasureTheory.pdf.quasiMeasurePreserving_hasPDF'

end

section Real

variable [IsFiniteMeasure ‚Ñô] {X : Œ© ‚Üí ‚Ñù}

/-- A real-valued random variable `X` `HasPDF X ‚Ñô Œª` (where `Œª` is the Lebesgue measure) if and
only if the push-forward measure of `‚Ñô` along `X` is absolutely continuous with respect to `Œª`. -/
nonrec theorem _root_.Real.hasPDF_iff_of_measurable (hX : Measurable X) :
    HasPDF X ‚Ñô ‚Üî map X ‚Ñô ‚â™ volume := by
  rw [hasPDF_iff_of_measurable hX]
  exact and_iff_right inferInstance
#align measure_theory.pdf.real.has_pdf_iff_of_measurable Real.hasPDF_iff_of_measurable

theorem _root_.Real.hasPDF_iff : HasPDF X ‚Ñô ‚Üî Measurable X ‚àß map X ‚Ñô ‚â™ volume := by
  by_cases hX : Measurable X
  ¬∑ rw [Real.hasPDF_iff_of_measurable hX, iff_and_self]
    exact fun _ => hX
  ¬∑ exact ‚ü®fun h => False.elim (hX h.pdf'.1), fun h => False.elim (hX h.1)‚ü©
#align measure_theory.pdf.real.has_pdf_iff Real.hasPDF_iff

/-- If `X` is a real-valued random variable that has pdf `f`, then the expectation of `X` equals
`‚à´ x, x * f x ‚àÇŒª` where `Œª` is the Lebesgue measure. -/
theorem integral_mul_eq_integral [HasPDF X ‚Ñô] : ‚à´ x, x * (pdf X ‚Ñô volume x).toReal = ‚à´ x, X x ‚àÇ‚Ñô :=
  integral_fun_mul_eq_integral measurable_id
#align measure_theory.pdf.integral_mul_eq_integral MeasureTheory.pdf.integral_mul_eq_integral

theorem hasFiniteIntegral_mul {f : ‚Ñù ‚Üí ‚Ñù} {g : ‚Ñù ‚Üí ‚Ñù‚â•0‚àû} (hg : pdf X ‚Ñô =·µê[volume] g)
    (hgi : ‚à´‚Åª x, ‚Äñf x‚Äñ‚Çä * g x ‚â† ‚àû) :
    HasFiniteIntegral fun x => f x * (pdf X ‚Ñô volume x).toReal := by
  rw [HasFiniteIntegral]
  have : (fun x => ‚Üë‚Äñf x‚Äñ‚Çä * g x) =·µê[volume] fun x => ‚Äñf x * (pdf X ‚Ñô volume x).toReal‚Äñ‚Çä := by
    refine' ae_eq_trans (Filter.EventuallyEq.mul (ae_eq_refl fun x => (‚Äñf x‚Äñ‚Çä : ‚Ñù‚â•0‚àû))
      (ae_eq_trans hg.symm ofReal_toReal_ae_eq.symm)) _
    simp_rw [‚Üê smul_eq_mul, nnnorm_smul, ENNReal.coe_mul, smul_eq_mul]
    refine' Filter.EventuallyEq.mul (ae_eq_refl _) _
    simp only [Real.ennnorm_eq_ofReal ENNReal.toReal_nonneg, ae_eq_refl]
  rwa [lt_top_iff_ne_top, ‚Üê lintegral_congr_ae this]
#align measure_theory.pdf.has_finite_integral_mul MeasureTheory.pdf.hasFiniteIntegral_mul

end Real

section

/-! **Uniform Distribution** -/

/-- A random variable `X` has uniform distribution if it has a probability density function `f`
with support `s` such that `f = (Œº s)‚Åª¬π 1‚Çõ` a.e. where `1‚Çõ` is the indicator function for `s`. -/
def IsUniform {_ : MeasurableSpace Œ©} (X : Œ© ‚Üí E) (support : Set E) (‚Ñô : Measure Œ©)
    (Œº : Measure E := by volume_tac) :=
  pdf X ‚Ñô Œº =·µê[Œº] support.indicator ((Œº support)‚Åª¬π ‚Ä¢ (1 : E ‚Üí ‚Ñù‚â•0‚àû))
#align measure_theory.pdf.is_uniform MeasureTheory.pdf.IsUniform

namespace IsUniform

theorem hasPDF {m : MeasurableSpace Œ©} {X : Œ© ‚Üí E} {‚Ñô : Measure Œ©} {Œº : Measure E} {s : Set E}
    (hns : Œº s ‚â† 0) (hnt : Œº s ‚â† ‚àû) (hu : IsUniform X s ‚Ñô Œº) : HasPDF X ‚Ñô Œº :=
  hasPDF_of_pdf_ne_zero
    (by
      intro hpdf
      simp only [IsUniform, hpdf] at hu
      suffices Œº (s ‚à© Function.support ((Œº s)‚Åª¬π ‚Ä¢ (1 : E ‚Üí ‚Ñù‚â•0‚àû))) = 0 by
        have heq : Function.support ((Œº s)‚Åª¬π ‚Ä¢ (1 : E ‚Üí ‚Ñù‚â•0‚àû)) = Set.univ := by
          ext x
          rw [Function.mem_support]
          simp [hnt]
        rw [heq, Set.inter_univ] at this
        exact hns this
      exact Set.indicator_ae_eq_zero.1 hu.symm)
#align measure_theory.pdf.is_uniform.has_pdf MeasureTheory.pdf.IsUniform.hasPDF

theorem pdf_toReal_ae_eq {_ : MeasurableSpace Œ©} {X : Œ© ‚Üí E} {‚Ñô : Measure Œ©} {Œº : Measure E}
    {s : Set E} (hX : IsUniform X s ‚Ñô Œº) :
    (fun x => (pdf X ‚Ñô Œº x).toReal) =·µê[Œº] fun x =>
      (s.indicator ((Œº s)‚Åª¬π ‚Ä¢ (1 : E ‚Üí ‚Ñù‚â•0‚àû)) x).toReal :=
  Filter.EventuallyEq.fun_comp hX ENNReal.toReal
#align measure_theory.pdf.is_uniform.pdf_to_real_ae_eq MeasureTheory.pdf.IsUniform.pdf_toReal_ae_eq

theorem measure_preimage {m : MeasurableSpace Œ©} {X : Œ© ‚Üí E} {‚Ñô : Measure Œ©} {Œº : Measure E}
    {s : Set E} (hns : Œº s ‚â† 0) (hnt : Œº s ‚â† ‚àû) (hms : MeasurableSet s) (hu : IsUniform X s ‚Ñô Œº)
    {A : Set E} (hA : MeasurableSet A) : ‚Ñô (X ‚Åª¬π' A) = Œº (s ‚à© A) / Œº s := by
  haveI := hu.hasPDF hns hnt
  rw [‚Üê Measure.map_apply (HasPDF.measurable X ‚Ñô Œº) hA, map_eq_set_lintegral_pdf X ‚Ñô Œº hA,
    lintegral_congr_ae hu.restrict]
  simp only [hms, hA, lintegral_indicator, Pi.smul_apply, Pi.one_apply, Algebra.id.smul_eq_mul,
    mul_one, lintegral_const, restrict_apply', Set.univ_inter]
  rw [ENNReal.div_eq_inv_mul]
#align measure_theory.pdf.is_uniform.measure_preimage MeasureTheory.pdf.IsUniform.measure_preimage

theorem isProbabilityMeasure {m : MeasurableSpace Œ©} {X : Œ© ‚Üí E} {‚Ñô : Measure Œ©} {Œº : Measure E}
    {s : Set E} (hns : Œº s ‚â† 0) (hnt : Œº s ‚â† ‚àû) (hms : MeasurableSet s) (hu : IsUniform X s ‚Ñô Œº) :
    IsProbabilityMeasure ‚Ñô :=
  ‚ü®by
    have : X ‚Åª¬π' Set.univ = Set.univ := by simp only [Set.preimage_univ]
    rw [‚Üê this, hu.measure_preimage hns hnt hms MeasurableSet.univ, Set.inter_univ,
      ENNReal.div_self hns hnt]‚ü©
#align measure_theory.pdf.is_uniform.is_probability_measure MeasureTheory.pdf.IsUniform.isProbabilityMeasure

variable {X : Œ© ‚Üí ‚Ñù} {s : Set ‚Ñù} (hms : MeasurableSet s) (hns : volume s ‚â† 0)

theorem mul_pdf_integrable [IsFiniteMeasure ‚Ñô] (hcs : IsCompact s) (huX : IsUniform X s ‚Ñô) :
    Integrable fun x : ‚Ñù => x * (pdf X ‚Ñô volume x).toReal := by
  by_cases hsupp : volume s = ‚àû
  ¬∑ have : pdf X ‚Ñô =·µê[volume] 0 := by
      refine' ae_eq_trans huX _
      simp [hsupp, ae_eq_refl]
    refine' Integrable.congr (integrable_zero _ _ _) _
    rw [(by simp : (fun x => 0 : ‚Ñù ‚Üí ‚Ñù) = fun x => x * (0 : ‚Ñù‚â•0‚àû).toReal)]
    refine'
      Filter.EventuallyEq.mul (ae_eq_refl _) (Filter.EventuallyEq.fun_comp this.symm ENNReal.toReal)
  constructor -- porting note: `refine` was failing, don't know why
  ¬∑ exact aestronglyMeasurable_id.mul
      (measurable_pdf X ‚Ñô).aemeasurable.ennreal_toReal.aestronglyMeasurable
  refine' hasFiniteIntegral_mul huX _
  set ind := (volume s)‚Åª¬π ‚Ä¢ (1 : ‚Ñù ‚Üí ‚Ñù‚â•0‚àû)
  have : ‚àÄ x, ‚Üë‚Äñx‚Äñ‚Çä * s.indicator ind x = s.indicator (fun x => ‚Äñx‚Äñ‚Çä * ind x) x := fun x =>
    (s.indicator_mul_right (fun x => ‚Üë‚Äñx‚Äñ‚Çä) ind).symm
  simp only [this, lintegral_indicator _ hms, mul_one, Algebra.id.smul_eq_mul, Pi.one_apply,
    Pi.smul_apply]
  rw [lintegral_mul_const _ measurable_nnnorm.coe_nnreal_ennreal]
  refine' (ENNReal.mul_lt_top (set_lintegral_lt_top_of_isCompact hsupp hcs continuous_nnnorm).ne
    (ENNReal.inv_lt_top.2 (pos_iff_ne_zero.mpr hns)).ne).ne
#align measure_theory.pdf.is_uniform.mul_pdf_integrable MeasureTheory.pdf.IsUniform.mul_pdf_integrable

/-- A real uniform random variable `X` with support `s` has expectation
`(Œª s)‚Åª¬π * ‚à´ x in s, x ‚àÇŒª` where `Œª` is the Lebesgue measure. -/
theorem integral_eq (hnt : volume s ‚â† ‚àû) (huX : IsUniform X s ‚Ñô) :
    ‚à´ x, X x ‚àÇ‚Ñô = (volume s)‚Åª¬π.toReal * ‚à´ x in s, x := by
  haveI := hasPDF hns hnt huX
  haveI := huX.isProbabilityMeasure hns hnt hms
  rw [‚Üê integral_mul_eq_integral]
  rw [integral_congr_ae (Filter.EventuallyEq.mul (ae_eq_refl _) (pdf_toReal_ae_eq huX))]
  have :
    ‚àÄ x,
      x * (s.indicator ((volume s)‚Åª¬π ‚Ä¢ (1 : ‚Ñù ‚Üí ‚Ñù‚â•0‚àû)) x).toReal =
        x * s.indicator ((volume s)‚Åª¬π.toReal ‚Ä¢ (1 : ‚Ñù ‚Üí ‚Ñù)) x := by
    refine' fun x => congr_arg ((¬∑ * ¬∑) x) _
    by_cases hx : x ‚àà s
    ¬∑ simp [Set.indicator_of_mem hx]
    ¬∑ simp [Set.indicator_of_not_mem hx]
  simp_rw [this, ‚Üê s.indicator_mul_right fun x => x, integral_indicator hms]
  change ‚à´ x in s, x * (volume s)‚Åª¬π.toReal ‚Ä¢ (1 : ‚Ñù) = _
  rw [integral_mul_right, mul_comm, smul_eq_mul, mul_one]
#align measure_theory.pdf.is_uniform.integral_eq MeasureTheory.pdf.IsUniform.integral_eq

end IsUniform

end

section TwoVariables

open ProbabilityTheory

variable {F : Type*} [MeasurableSpace F] {ŒΩ : Measure F} {X : Œ© ‚Üí E} {Y : Œ© ‚Üí F}

/-- Random variables are independent iff their joint density is a product of marginal densities. -/
theorem indepFun_iff_pdf_prod_eq_pdf_mul_pdf
    [IsFiniteMeasure ‚Ñô] [SigmaFinite Œº] [SigmaFinite ŒΩ] [HasPDF (fun œâ ‚Ü¶ (X œâ, Y œâ)) ‚Ñô (Œº.prod ŒΩ)] :
    IndepFun X Y ‚Ñô ‚Üî
      pdf (fun œâ ‚Ü¶ (X œâ, Y œâ)) ‚Ñô (Œº.prod ŒΩ) =·µê[Œº.prod ŒΩ] fun z ‚Ü¶ pdf X ‚Ñô Œº z.1 * pdf Y ‚Ñô ŒΩ z.2 := by
  have : HasPDF X ‚Ñô Œº := quasiMeasurePreserving_hasPDF' (Œº := Œº.prod ŒΩ) (X := fun œâ ‚Ü¶ (X œâ, Y œâ))
    quasiMeasurePreserving_fst
  have : HasPDF Y ‚Ñô ŒΩ := quasiMeasurePreserving_hasPDF' (Œº := Œº.prod ŒΩ) (X := fun œâ ‚Ü¶ (X œâ, Y œâ))
    quasiMeasurePreserving_snd
  have h‚ÇÄ : (‚Ñô.map X).prod (‚Ñô.map Y) =
      (Œº.prod ŒΩ).withDensity fun z ‚Ü¶ pdf X ‚Ñô Œº z.1 * pdf Y ‚Ñô ŒΩ z.2 :=
    prod_eq fun s t hs ht ‚Ü¶ by rw [withDensity_apply _ (hs.prod ht), ‚Üê prod_restrict,
      lintegral_prod_mul (measurable_pdf X ‚Ñô Œº).aemeasurable (measurable_pdf Y ‚Ñô ŒΩ).aemeasurable,
      map_eq_set_lintegral_pdf X ‚Ñô Œº hs, map_eq_set_lintegral_pdf Y ‚Ñô ŒΩ ht]
  rw [indepFun_iff_map_prod_eq_prod_map_map (HasPDF.measurable X ‚Ñô Œº) (HasPDF.measurable Y ‚Ñô ŒΩ),
    ‚Üê unique, h‚ÇÄ]
  exact (((measurable_pdf X ‚Ñô Œº).comp measurable_fst).mul
    ((measurable_pdf Y ‚Ñô ŒΩ).comp measurable_snd)).aemeasurable

end TwoVariables

end pdf

end MeasureTheory
