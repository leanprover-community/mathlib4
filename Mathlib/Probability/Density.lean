/-
Copyright (c) 2021 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import Mathlib.MeasureTheory.Decomposition.RadonNikodym
import Mathlib.MeasureTheory.Measure.Haar.OfBasis
import Mathlib.Probability.Independence.Basic

#align_import probability.density from "leanprover-community/mathlib"@"c14c8fcde993801fca8946b0d80131a1a81d1520"

/-!
# Probability density function

This file defines the probability density function of random variables, by which we mean
measurable functions taking values in a Borel space. The probability density function is defined
as the Radon‚ÄìNikodym derivative of the law of `X`. In particular, a measurable function `f`
is said to the probability density function of a random variable `X` if for all measurable
sets `S`, `‚Ñô(X ‚àà S) = ‚à´ x in S, f x dx`. Probability density functions are one way of describing
the distribution of a random variable, and are useful for calculating probabilities and
finding moments (although the latter is better achieved with moment generating functions).

This file also defines the continuous uniform distribution and proves some properties about
random variables with this distribution.

## Main definitions

* `MeasureTheory.HasPDF` : A random variable `X : Œ© ‚Üí E` is said to `HasPDF` with
  respect to the measure `‚Ñô` on `Œ©` and `Œº` on `E` if the push-forward measure of `‚Ñô` along `X`
  is absolutely continuous with respect to `Œº` and they `HaveLebesgueDecomposition`.
* `MeasureTheory.pdf` : If `X` is a random variable that `HasPDF X ‚Ñô Œº`, then `pdf X`
  is the Radon‚ÄìNikodym derivative of the push-forward measure of `‚Ñô` along `X` with respect to `Œº`.
* `MeasureTheory.pdf.IsUniform` : A random variable `X` is said to follow the uniform
  distribution if it has a constant probability density function with a compact, non-null support.

## Main results

* `MeasureTheory.pdf.integral_pdf_smul` : Law of the unconscious statistician,
  i.e. if a random variable `X : Œ© ‚Üí E` has pdf `f`, then `ùîº(g(X)) = ‚à´ x, f x ‚Ä¢ g x dx` for
  all measurable `g : E ‚Üí F`.
* `MeasureTheory.pdf.integral_mul_eq_integral` : A real-valued random variable `X` with
  pdf `f` has expectation `‚à´ x, x * f x dx`.
* `MeasureTheory.pdf.IsUniform.integral_eq` : If `X` follows the uniform distribution with
  its pdf having support `s`, then `X` has expectation `(Œª s)‚Åª¬π * ‚à´ x in s, x dx` where `Œª`
  is the Lebesgue measure.

## TODOs

Ultimately, we would also like to define characteristic functions to describe distributions as
it exists for all random variables. However, to define this, we will need Fourier transforms
which we currently do not have.
-/


open scoped Classical MeasureTheory NNReal ENNReal

open TopologicalSpace MeasureTheory.Measure

noncomputable section

namespace MeasureTheory

variable {Œ© E : Type*} [MeasurableSpace E]

/-- A random variable `X : Œ© ‚Üí E` is said to `HasPDF` with respect to the measure `‚Ñô` on `Œ©` and
`Œº` on `E` if the push-forward measure of `‚Ñô` along `X` is absolutely continuous with respect to
`Œº` and they `HaveLebesgueDecomposition`. -/
class HasPDF {m : MeasurableSpace Œ©} (X : Œ© ‚Üí E) (‚Ñô : Measure Œ©)
    (Œº : Measure E := by volume_tac) : Prop where
  pdf' : AEMeasurable X ‚Ñô ‚àß (map X ‚Ñô).HaveLebesgueDecomposition Œº ‚àß map X ‚Ñô ‚â™ Œº
#align measure_theory.has_pdf MeasureTheory.HasPDF

section HasPDF

variable {_ : MeasurableSpace Œ©}

theorem hasPDF_iff {X : Œ© ‚Üí E} {‚Ñô : Measure Œ©} {Œº : Measure E} :
    HasPDF X ‚Ñô Œº ‚Üî AEMeasurable X ‚Ñô ‚àß (map X ‚Ñô).HaveLebesgueDecomposition Œº ‚àß map X ‚Ñô ‚â™ Œº :=
  ‚ü®@HasPDF.pdf' _ _ _ _ _ _ _, HasPDF.mk‚ü©
#align measure_theory.pdf.has_pdf_iff MeasureTheory.hasPDF_iff

theorem hasPDF_iff_of_aemeasurable {X : Œ© ‚Üí E} {‚Ñô : Measure Œ©}
    {Œº : Measure E} (hX : AEMeasurable X ‚Ñô) :
    HasPDF X ‚Ñô Œº ‚Üî (map X ‚Ñô).HaveLebesgueDecomposition Œº ‚àß map X ‚Ñô ‚â™ Œº := by
  rw [hasPDF_iff]
  simp only [hX, true_and]
#align measure_theory.pdf.has_pdf_iff_of_measurable MeasureTheory.hasPDF_iff_of_aemeasurable

@[measurability]
theorem HasPDF.aemeasurable (X : Œ© ‚Üí E) (‚Ñô : Measure Œ©)
    (Œº : Measure E) [hX : HasPDF X ‚Ñô Œº] : AEMeasurable X ‚Ñô :=
  hX.pdf'.1
#align measure_theory.has_pdf.measurable MeasureTheory.HasPDF.aemeasurable

instance HasPDF.haveLebesgueDecomposition {X : Œ© ‚Üí E} {‚Ñô : Measure Œ©}
    {Œº : Measure E} [hX : HasPDF X ‚Ñô Œº] : (map X ‚Ñô).HaveLebesgueDecomposition Œº :=
  hX.pdf'.2.1
#align measure_theory.pdf.have_lebesgue_decomposition_of_has_pdf MeasureTheory.HasPDF.haveLebesgueDecomposition

theorem HasPDF.absolutelyContinuous {X : Œ© ‚Üí E} {‚Ñô : Measure Œ©} {Œº : Measure E}
    [hX : HasPDF X ‚Ñô Œº] : map X ‚Ñô ‚â™ Œº :=
  hX.pdf'.2.2
#align measure_theory.pdf.map_absolutely_continuous MeasureTheory.HasPDF.absolutelyContinuous

/-- A random variable that `HasPDF` is quasi-measure preserving. -/
theorem HasPDF.quasiMeasurePreserving_of_measurable (X : Œ© ‚Üí E) (‚Ñô : Measure Œ©) (Œº : Measure E)
    [HasPDF X ‚Ñô Œº] (h : Measurable X) : QuasiMeasurePreserving X ‚Ñô Œº :=
  { measurable := h
    absolutelyContinuous := HasPDF.absolutelyContinuous }
#align measure_theory.pdf.to_quasi_measure_preserving MeasureTheory.HasPDF.quasiMeasurePreserving_of_measurable

theorem HasPDF.congr {X Y : Œ© ‚Üí E} {‚Ñô : Measure Œ©} {Œº : Measure E} (hXY : X =·µê[‚Ñô] Y)
    [hX : HasPDF X ‚Ñô Œº] : HasPDF Y ‚Ñô Œº :=
  ‚ü®(HasPDF.aemeasurable X ‚Ñô Œº).congr hXY, ‚Ñô.map_congr hXY ‚ñ∏ hX.haveLebesgueDecomposition,
    ‚Ñô.map_congr hXY ‚ñ∏ hX.absolutelyContinuous‚ü©

theorem HasPDF.congr' {X Y : Œ© ‚Üí E} {‚Ñô : Measure Œ©} {Œº : Measure E} (hXY : X =·µê[‚Ñô] Y) :
    HasPDF X ‚Ñô Œº ‚Üî HasPDF Y ‚Ñô Œº :=
  ‚ü®fun _ ‚Ü¶ HasPDF.congr hXY, fun _ ‚Ü¶ HasPDF.congr hXY.symm‚ü©

/-- X `HasPDF` if there is a pdf `f` such that `map X ‚Ñô = Œº.withDensity f`. -/
theorem hasPDF_of_map_eq_withDensity {X : Œ© ‚Üí E} {‚Ñô : Measure Œ©} {Œº : Measure E}
    (hX : AEMeasurable X ‚Ñô) (f : E ‚Üí ‚Ñù‚â•0‚àû) (hf : AEMeasurable f Œº) (h : map X ‚Ñô = Œº.withDensity f) :
    HasPDF X ‚Ñô Œº := by
  refine ‚ü®hX, ?_, ?_‚ü© <;> rw [h]
  ¬∑ rw [withDensity_congr_ae hf.ae_eq_mk]
    exact haveLebesgueDecomposition_withDensity Œº hf.measurable_mk
  ¬∑ exact withDensity_absolutelyContinuous Œº f

end HasPDF

/-- If `X` is a random variable, then `pdf X` is the Radon‚ÄìNikodym derivative of the push-forward
measure of `‚Ñô` along `X` with respect to `Œº`. -/
def pdf {_ : MeasurableSpace Œ©} (X : Œ© ‚Üí E) (‚Ñô : Measure Œ©) (Œº : Measure E := by volume_tac) :
    E ‚Üí ‚Ñù‚â•0‚àû :=
  (map X ‚Ñô).rnDeriv Œº
#align measure_theory.pdf MeasureTheory.pdf

theorem pdf_def {_ : MeasurableSpace Œ©} {‚Ñô : Measure Œ©} {Œº : Measure E} {X : Œ© ‚Üí E} :
    pdf X ‚Ñô Œº = (map X ‚Ñô).rnDeriv Œº := rfl

theorem pdf_of_not_aemeasurable {_ : MeasurableSpace Œ©} {‚Ñô : Measure Œ©} {Œº : Measure E}
    {X : Œ© ‚Üí E} (hX : ¬¨AEMeasurable X ‚Ñô) : pdf X ‚Ñô Œº =·µê[Œº] 0 := by
  rw [pdf_def, map_of_not_aemeasurable hX]
  exact rnDeriv_zero Œº
#align measure_theory.pdf_eq_zero_of_not_measurable MeasureTheory.pdf_of_not_aemeasurable

theorem pdf_of_not_haveLebesgueDecomposition {_ : MeasurableSpace Œ©} {‚Ñô : Measure Œ©}
    {Œº : Measure E} {X : Œ© ‚Üí E} (h : ¬¨(map X ‚Ñô).HaveLebesgueDecomposition Œº) : pdf X ‚Ñô Œº = 0 :=
  rnDeriv_of_not_haveLebesgueDecomposition h

theorem aemeasurable_of_pdf_ne_zero {m : MeasurableSpace Œ©} {‚Ñô : Measure Œ©} {Œº : Measure E}
    (X : Œ© ‚Üí E) (h : ¬¨pdf X ‚Ñô Œº =·µê[Œº] 0) : AEMeasurable X ‚Ñô := by
  contrapose! h
  exact pdf_of_not_aemeasurable h
#align measure_theory.measurable_of_pdf_ne_zero MeasureTheory.aemeasurable_of_pdf_ne_zero

theorem hasPDF_of_pdf_ne_zero {m : MeasurableSpace Œ©} {‚Ñô : Measure Œ©} {Œº : Measure E} {X : Œ© ‚Üí E}
    (hac : map X ‚Ñô ‚â™ Œº) (hpdf : ¬¨pdf X ‚Ñô Œº =·µê[Œº] 0) : HasPDF X ‚Ñô Œº := by
  refine ‚ü®?_, ?_, hac‚ü©
  ¬∑ exact aemeasurable_of_pdf_ne_zero X hpdf
  ¬∑ contrapose! hpdf
    have := pdf_of_not_haveLebesgueDecomposition hpdf
    filter_upwards using congrFun this
#align measure_theory.has_pdf_of_pdf_ne_zero MeasureTheory.hasPDF_of_pdf_ne_zero

@[measurability]
theorem measurable_pdf {m : MeasurableSpace Œ©} (X : Œ© ‚Üí E) (‚Ñô : Measure Œ©)
    (Œº : Measure E := by volume_tac) : Measurable (pdf X ‚Ñô Œº) := by
  exact measurable_rnDeriv _ _
#align measure_theory.measurable_pdf MeasureTheory.measurable_pdf

theorem withDensity_pdf_le_map {_ : MeasurableSpace Œ©} (X : Œ© ‚Üí E) (‚Ñô : Measure Œ©)
    (Œº : Measure E := by volume_tac) : Œº.withDensity (pdf X ‚Ñô Œº) ‚â§ map X ‚Ñô :=
  withDensity_rnDeriv_le _ _

theorem set_lintegral_pdf_le_map {m : MeasurableSpace Œ©} (X : Œ© ‚Üí E) (‚Ñô : Measure Œ©)
    (Œº : Measure E := by volume_tac) (s : Set E) :
    ‚à´‚Åª x in s, pdf X ‚Ñô Œº x ‚àÇŒº ‚â§ map X ‚Ñô s := by
  apply (withDensity_apply_le _ s).trans
  exact withDensity_pdf_le_map _ _ _ s

theorem map_eq_withDensity_pdf {m : MeasurableSpace Œ©} (X : Œ© ‚Üí E) (‚Ñô : Measure Œ©)
    (Œº : Measure E := by volume_tac) [hX : HasPDF X ‚Ñô Œº] :
    map X ‚Ñô = Œº.withDensity (pdf X ‚Ñô Œº) := by
  rw [pdf_def, withDensity_rnDeriv_eq _ _ hX.absolutelyContinuous]
#align measure_theory.map_eq_with_density_pdf MeasureTheory.map_eq_withDensity_pdf

theorem map_eq_set_lintegral_pdf {m : MeasurableSpace Œ©} (X : Œ© ‚Üí E) (‚Ñô : Measure Œ©)
    (Œº : Measure E := by volume_tac) [hX : HasPDF X ‚Ñô Œº] {s : Set E}
    (hs : MeasurableSet s) : map X ‚Ñô s = ‚à´‚Åª x in s, pdf X ‚Ñô Œº x ‚àÇŒº := by
  rw [‚Üê withDensity_apply _ hs, map_eq_withDensity_pdf X ‚Ñô Œº]
#align measure_theory.map_eq_set_lintegral_pdf MeasureTheory.map_eq_set_lintegral_pdf

namespace pdf

variable {m : MeasurableSpace Œ©} {‚Ñô : Measure Œ©} {Œº : Measure E}

protected theorem congr {X Y : Œ© ‚Üí E} (hXY : X =·µê[‚Ñô] Y) : pdf X ‚Ñô Œº = pdf Y ‚Ñô Œº := by
  rw [pdf_def, pdf_def, map_congr hXY]

theorem lintegral_eq_measure_univ {X : Œ© ‚Üí E} [HasPDF X ‚Ñô Œº] :
    ‚à´‚Åª x, pdf X ‚Ñô Œº x ‚àÇŒº = ‚Ñô Set.univ := by
  rw [‚Üê set_lintegral_univ, ‚Üê map_eq_set_lintegral_pdf X ‚Ñô Œº MeasurableSet.univ,
    map_apply_of_aemeasurable (HasPDF.aemeasurable X ‚Ñô Œº) MeasurableSet.univ, Set.preimage_univ]
#align measure_theory.pdf.lintegral_eq_measure_univ MeasureTheory.pdf.lintegral_eq_measure_univ

theorem eq_of_map_eq_withDensity [IsFiniteMeasure ‚Ñô] {X : Œ© ‚Üí E} [HasPDF X ‚Ñô Œº] (f : E ‚Üí ‚Ñù‚â•0‚àû)
    (hmf : AEMeasurable f Œº) : map X ‚Ñô = Œº.withDensity f ‚Üî pdf X ‚Ñô Œº =·µê[Œº] f := by
  rw [map_eq_withDensity_pdf X ‚Ñô Œº]
  apply withDensity_eq_iff (measurable_pdf X ‚Ñô Œº).aemeasurable hmf
  rw [lintegral_eq_measure_univ]
  exact measure_ne_top _ _

theorem eq_of_map_eq_withDensity' [SigmaFinite Œº] {X : Œ© ‚Üí E} [HasPDF X ‚Ñô Œº] (f : E ‚Üí ‚Ñù‚â•0‚àû)
    (hmf : AEMeasurable f Œº) : map X ‚Ñô = Œº.withDensity f ‚Üî pdf X ‚Ñô Œº =·µê[Œº] f :=
  map_eq_withDensity_pdf X ‚Ñô Œº ‚ñ∏
    withDensity_eq_iff_of_sigmaFinite (measurable_pdf X ‚Ñô Œº).aemeasurable hmf

nonrec theorem ae_lt_top [IsFiniteMeasure ‚Ñô] {Œº : Measure E} {X : Œ© ‚Üí E} :
    ‚àÄ·µê x ‚àÇŒº, pdf X ‚Ñô Œº x < ‚àû :=
  rnDeriv_lt_top (map X ‚Ñô) Œº
#align measure_theory.pdf.ae_lt_top MeasureTheory.pdf.ae_lt_top

nonrec theorem ofReal_toReal_ae_eq [IsFiniteMeasure ‚Ñô] {X : Œ© ‚Üí E} :
    (fun x => ENNReal.ofReal (pdf X ‚Ñô Œº x).toReal) =·µê[Œº] pdf X ‚Ñô Œº :=
  ofReal_toReal_ae_eq ae_lt_top
#align measure_theory.pdf.of_real_to_real_ae_eq MeasureTheory.pdf.ofReal_toReal_ae_eq

section IntegralPDFMul

/-- **The Law of the Unconscious Statistician** for nonnegative random variables. -/
theorem lintegral_pdf_mul {X : Œ© ‚Üí E} [HasPDF X ‚Ñô Œº] {f : E ‚Üí ‚Ñù‚â•0‚àû}
    (hf : AEMeasurable f Œº) : ‚à´‚Åª x, pdf X ‚Ñô Œº x * f x ‚àÇŒº = ‚à´‚Åª x, f (X x) ‚àÇ‚Ñô := by
  rw [pdf_def,
    ‚Üê lintegral_map' (hf.mono_ac HasPDF.absolutelyContinuous) (HasPDF.aemeasurable X ‚Ñô Œº),
    lintegral_rnDeriv_mul HasPDF.absolutelyContinuous hf]

variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ‚Ñù F] [CompleteSpace F]

theorem integrable_pdf_smul_iff [IsFiniteMeasure ‚Ñô] {X : Œ© ‚Üí E} [HasPDF X ‚Ñô Œº] {f : E ‚Üí F}
    (hf : AEStronglyMeasurable f Œº) :
    Integrable (fun x => (pdf X ‚Ñô Œº x).toReal ‚Ä¢ f x) Œº ‚Üî Integrable (fun x => f (X x)) ‚Ñô := by
  -- Porting note: using `erw` because `rw` doesn't recognize `(f <| X ¬∑)` as `f ‚àò X`
  -- https://github.com/leanprover-community/mathlib4/issues/5164
  erw [‚Üê integrable_map_measure (hf.mono_ac HasPDF.absolutelyContinuous)
    (HasPDF.aemeasurable X ‚Ñô Œº),
    map_eq_withDensity_pdf X ‚Ñô Œº, pdf_def, integrable_rnDeriv_smul_iff HasPDF.absolutelyContinuous]
  eta_reduce
  rw [withDensity_rnDeriv_eq _ _ HasPDF.absolutelyContinuous]
#align measure_theory.pdf.integrable_iff_integrable_mul_pdf MeasureTheory.pdf.integrable_pdf_smul_iff

/-- **The Law of the Unconscious Statistician**: Given a random variable `X` and a measurable
function `f`, `f ‚àò X` is a random variable with expectation `‚à´ x, pdf X x ‚Ä¢ f x ‚àÇŒº`
where `Œº` is a measure on the codomain of `X`. -/
theorem integral_pdf_smul [IsFiniteMeasure ‚Ñô] {X : Œ© ‚Üí E} [HasPDF X ‚Ñô Œº] {f : E ‚Üí F}
    (hf : AEStronglyMeasurable f Œº) : ‚à´ x, (pdf X ‚Ñô Œº x).toReal ‚Ä¢ f x ‚àÇŒº = ‚à´ x, f (X x) ‚àÇ‚Ñô := by
  rw [‚Üê integral_map (HasPDF.aemeasurable X ‚Ñô Œº) (hf.mono_ac HasPDF.absolutelyContinuous),
    map_eq_withDensity_pdf X ‚Ñô Œº, pdf_def, integral_rnDeriv_smul HasPDF.absolutelyContinuous,
    withDensity_rnDeriv_eq _ _ HasPDF.absolutelyContinuous]
#align measure_theory.pdf.integral_fun_mul_eq_integral MeasureTheory.pdf.integral_pdf_smul

end IntegralPDFMul

section

variable {F : Type*} [MeasurableSpace F] {ŒΩ : Measure F}

/-- A random variable that `HasPDF` transformed under a `QuasiMeasurePreserving`
map also `HasPDF` if `(map g (map X ‚Ñô)).HaveLebesgueDecomposition Œº`.

`quasiMeasurePreserving_hasPDF` is more useful in the case we are working with a
probability measure and a real-valued random variable. -/
theorem quasiMeasurePreserving_hasPDF {X : Œ© ‚Üí E} [HasPDF X ‚Ñô Œº] (hX : AEMeasurable X ‚Ñô) {g : E ‚Üí F}
    (hg : QuasiMeasurePreserving g Œº ŒΩ) (hmap : (map g (map X ‚Ñô)).HaveLebesgueDecomposition ŒΩ) :
    HasPDF (g ‚àò X) ‚Ñô ŒΩ := by
  wlog hmX : Measurable X
  ¬∑ have hae : g ‚àò X =·µê[‚Ñô] g ‚àò hX.mk := hX.ae_eq_mk.mono fun x h ‚Ü¶ by dsimp; rw [h]
    have hXmk : HasPDF hX.mk ‚Ñô Œº := HasPDF.congr hX.ae_eq_mk
    apply (HasPDF.congr' hae).mpr
    exact this hX.measurable_mk.aemeasurable hg (map_congr hX.ae_eq_mk ‚ñ∏ hmap) hX.measurable_mk
  rw [hasPDF_iff, ‚Üê map_map hg.measurable hmX]
  refine ‚ü®(hg.measurable.comp hmX).aemeasurable, hmap, ?_‚ü©
  rw [map_eq_withDensity_pdf X ‚Ñô Œº]
  refine AbsolutelyContinuous.mk fun s hsm hs => ?_
  rw [map_apply hg.measurable hsm, withDensity_apply _ (hg.measurable hsm)]
  have := hg.absolutelyContinuous hs
  rw [map_apply hg.measurable hsm] at this
  exact set_lintegral_measure_zero _ _ this
#align measure_theory.pdf.quasi_measure_preserving_has_pdf MeasureTheory.pdf.quasiMeasurePreserving_hasPDF

theorem quasiMeasurePreserving_hasPDF' [IsFiniteMeasure ‚Ñô] [SigmaFinite ŒΩ] {X : Œ© ‚Üí E}
    [HasPDF X ‚Ñô Œº] (hX : AEMeasurable X ‚Ñô) {g : E ‚Üí F} (hg : QuasiMeasurePreserving g Œº ŒΩ) :
    HasPDF (g ‚àò X) ‚Ñô ŒΩ :=
  quasiMeasurePreserving_hasPDF hX hg inferInstance
#align measure_theory.pdf.quasi_measure_preserving_has_pdf' MeasureTheory.pdf.quasiMeasurePreserving_hasPDF'

end

section Real

variable [IsFiniteMeasure ‚Ñô] {X : Œ© ‚Üí ‚Ñù}

/-- A real-valued random variable `X` `HasPDF X ‚Ñô Œª` (where `Œª` is the Lebesgue measure) if and
only if the push-forward measure of `‚Ñô` along `X` is absolutely continuous with respect to `Œª`. -/
nonrec theorem _root_.Real.hasPDF_iff_of_aemeasurable (hX : AEMeasurable X ‚Ñô) :
    HasPDF X ‚Ñô ‚Üî map X ‚Ñô ‚â™ volume := by
  rw [hasPDF_iff_of_aemeasurable hX]
  exact and_iff_right inferInstance
#align measure_theory.pdf.real.has_pdf_iff_of_measurable Real.hasPDF_iff_of_aemeasurable

theorem _root_.Real.hasPDF_iff : HasPDF X ‚Ñô ‚Üî AEMeasurable X ‚Ñô ‚àß map X ‚Ñô ‚â™ volume := by
  by_cases hX : AEMeasurable X ‚Ñô
  ¬∑ rw [Real.hasPDF_iff_of_aemeasurable hX, iff_and_self]
    exact fun _ => hX
  ¬∑ exact ‚ü®fun h => False.elim (hX h.pdf'.1), fun h => False.elim (hX h.1)‚ü©
#align measure_theory.pdf.real.has_pdf_iff Real.hasPDF_iff

/-- If `X` is a real-valued random variable that has pdf `f`, then the expectation of `X` equals
`‚à´ x, x * f x ‚àÇŒª` where `Œª` is the Lebesgue measure. -/
theorem integral_mul_eq_integral [HasPDF X ‚Ñô] : ‚à´ x, x * (pdf X ‚Ñô volume x).toReal = ‚à´ x, X x ‚àÇ‚Ñô :=
  calc
    _ = ‚à´ x, (pdf X ‚Ñô volume x).toReal * x := by congr with x; exact mul_comm _ _
    _ = _ := integral_pdf_smul measurable_id.aestronglyMeasurable
#align measure_theory.pdf.integral_mul_eq_integral MeasureTheory.pdf.integral_mul_eq_integral

theorem hasFiniteIntegral_mul {f : ‚Ñù ‚Üí ‚Ñù} {g : ‚Ñù ‚Üí ‚Ñù‚â•0‚àû} (hg : pdf X ‚Ñô =·µê[volume] g)
    (hgi : ‚à´‚Åª x, ‚Äñf x‚Äñ‚Çä * g x ‚â† ‚àû) :
    HasFiniteIntegral fun x => f x * (pdf X ‚Ñô volume x).toReal := by
  rw [HasFiniteIntegral]
  have : (fun x => ‚Üë‚Äñf x‚Äñ‚Çä * g x) =·µê[volume] fun x => ‚Äñf x * (pdf X ‚Ñô volume x).toReal‚Äñ‚Çä := by
    refine ae_eq_trans (Filter.EventuallyEq.mul (ae_eq_refl fun x => (‚Äñf x‚Äñ‚Çä : ‚Ñù‚â•0‚àû))
      (ae_eq_trans hg.symm ofReal_toReal_ae_eq.symm)) ?_
    simp_rw [‚Üê smul_eq_mul, nnnorm_smul, ENNReal.coe_mul, smul_eq_mul]
    refine Filter.EventuallyEq.mul (ae_eq_refl _) ?_
    simp only [Real.ennnorm_eq_ofReal ENNReal.toReal_nonneg, ae_eq_refl]
  rwa [lt_top_iff_ne_top, ‚Üê lintegral_congr_ae this]
#align measure_theory.pdf.has_finite_integral_mul MeasureTheory.pdf.hasFiniteIntegral_mul

end Real

section TwoVariables

open ProbabilityTheory

variable {F : Type*} [MeasurableSpace F] {ŒΩ : Measure F} {X : Œ© ‚Üí E} {Y : Œ© ‚Üí F}

/-- Random variables are independent iff their joint density is a product of marginal densities. -/
theorem indepFun_iff_pdf_prod_eq_pdf_mul_pdf
    [IsFiniteMeasure ‚Ñô] [SigmaFinite Œº] [SigmaFinite ŒΩ] [HasPDF (fun œâ ‚Ü¶ (X œâ, Y œâ)) ‚Ñô (Œº.prod ŒΩ)] :
    IndepFun X Y ‚Ñô ‚Üî
      pdf (fun œâ ‚Ü¶ (X œâ, Y œâ)) ‚Ñô (Œº.prod ŒΩ) =·µê[Œº.prod ŒΩ] fun z ‚Ü¶ pdf X ‚Ñô Œº z.1 * pdf Y ‚Ñô ŒΩ z.2 := by
  have : HasPDF X ‚Ñô Œº := quasiMeasurePreserving_hasPDF' (Œº := Œº.prod ŒΩ)
    (HasPDF.aemeasurable (fun œâ ‚Ü¶ (X œâ, Y œâ)) ‚Ñô (Œº.prod ŒΩ)) quasiMeasurePreserving_fst
  have : HasPDF Y ‚Ñô ŒΩ := quasiMeasurePreserving_hasPDF' (Œº := Œº.prod ŒΩ)
    (HasPDF.aemeasurable (fun œâ ‚Ü¶ (X œâ, Y œâ)) ‚Ñô (Œº.prod ŒΩ)) quasiMeasurePreserving_snd
  have h‚ÇÄ : (‚Ñô.map X).prod (‚Ñô.map Y) =
      (Œº.prod ŒΩ).withDensity fun z ‚Ü¶ pdf X ‚Ñô Œº z.1 * pdf Y ‚Ñô ŒΩ z.2 :=
    prod_eq fun s t hs ht ‚Ü¶ by rw [withDensity_apply _ (hs.prod ht), ‚Üê prod_restrict,
      lintegral_prod_mul (measurable_pdf X ‚Ñô Œº).aemeasurable (measurable_pdf Y ‚Ñô ŒΩ).aemeasurable,
      map_eq_set_lintegral_pdf X ‚Ñô Œº hs, map_eq_set_lintegral_pdf Y ‚Ñô ŒΩ ht]
  rw [indepFun_iff_map_prod_eq_prod_map_map (HasPDF.aemeasurable X ‚Ñô Œº) (HasPDF.aemeasurable Y ‚Ñô ŒΩ),
    ‚Üê eq_of_map_eq_withDensity, h‚ÇÄ]
  exact (((measurable_pdf X ‚Ñô Œº).comp measurable_fst).mul
    ((measurable_pdf Y ‚Ñô ŒΩ).comp measurable_snd)).aemeasurable

end TwoVariables

end pdf

end MeasureTheory
