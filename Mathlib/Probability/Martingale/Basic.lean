/-
Copyright (c) 2021 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne, Kexing Ying
-/
import Mathlib.Probability.Notation
import Mathlib.Probability.Process.Stopping
import Mathlib.Probability.Process.Predictable

/-!
# Martingales

A family of functions `f : Œπ ‚Üí Œ© ‚Üí E` is a martingale with respect to a filtration `‚Ñ±` if every
`f i` is integrable, `f` is adapted with respect to `‚Ñ±` and for all `i ‚â§ j`,
`Œº[f j | ‚Ñ± i] =·µê[Œº] f i`. On the other hand, `f : Œπ ‚Üí Œ© ‚Üí E` is said to be a supermartingale
with respect to the filtration `‚Ñ±` if `f i` is integrable, `f` is adapted with respect to `‚Ñ±`
and for all `i ‚â§ j`, `Œº[f j | ‚Ñ± i] ‚â§·µê[Œº] f i`. Finally, `f : Œπ ‚Üí Œ© ‚Üí E` is said to be a
submartingale with respect to the filtration `‚Ñ±` if `f i` is integrable, `f` is adapted with
respect to `‚Ñ±` and for all `i ‚â§ j`, `f i ‚â§·µê[Œº] Œº[f j | ‚Ñ± i]`.

The definitions of filtration and adapted can be found in `Probability.Process.Stopping`.

### Definitions

* `MeasureTheory.Martingale f ‚Ñ± Œº`: `f` is a martingale with respect to filtration `‚Ñ±` and
  measure `Œº`.
* `MeasureTheory.Supermartingale f ‚Ñ± Œº`: `f` is a supermartingale with respect to
  filtration `‚Ñ±` and measure `Œº`.
* `MeasureTheory.Submartingale f ‚Ñ± Œº`: `f` is a submartingale with respect to filtration `‚Ñ±` and
  measure `Œº`.

### Results

* `MeasureTheory.martingale_condExp f ‚Ñ± Œº`: the sequence `fun i => Œº[f | ‚Ñ± i, ‚Ñ±.le i])` is a
  martingale with respect to `‚Ñ±` and `Œº`.

-/


open TopologicalSpace Filter

open scoped NNReal ENNReal MeasureTheory ProbabilityTheory

namespace MeasureTheory

variable {Œ© E Œπ : Type*} [Preorder Œπ] {m0 : MeasurableSpace Œ©} {Œº : Measure Œ©}
  [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [CompleteSpace E] {f g : Œπ ‚Üí Œ© ‚Üí E} {‚Ñ± : Filtration Œπ m0}

/-- A family of functions `f : Œπ ‚Üí Œ© ‚Üí E` is a martingale with respect to a filtration `‚Ñ±` if `f`
is adapted with respect to `‚Ñ±` and for all `i ‚â§ j`, `Œº[f j | ‚Ñ± i] =·µê[Œº] f i`. -/
def Martingale (f : Œπ ‚Üí Œ© ‚Üí E) (‚Ñ± : Filtration Œπ m0) (Œº : Measure Œ©) : Prop :=
  Adapted ‚Ñ± f ‚àß ‚àÄ i j, i ‚â§ j ‚Üí Œº[f j|‚Ñ± i] =·µê[Œº] f i

/-- A family of integrable functions `f : Œπ ‚Üí Œ© ‚Üí E` is a supermartingale with respect to a
filtration `‚Ñ±` if `f` is adapted with respect to `‚Ñ±` and for all `i ‚â§ j`,
`Œº[f j | ‚Ñ±.le i] ‚â§·µê[Œº] f i`. -/
def Supermartingale [LE E] (f : Œπ ‚Üí Œ© ‚Üí E) (‚Ñ± : Filtration Œπ m0) (Œº : Measure Œ©) : Prop :=
  Adapted ‚Ñ± f ‚àß (‚àÄ i j, i ‚â§ j ‚Üí Œº[f j|‚Ñ± i] ‚â§·µê[Œº] f i) ‚àß ‚àÄ i, Integrable (f i) Œº

/-- A family of integrable functions `f : Œπ ‚Üí Œ© ‚Üí E` is a submartingale with respect to a
filtration `‚Ñ±` if `f` is adapted with respect to `‚Ñ±` and for all `i ‚â§ j`,
`f i ‚â§·µê[Œº] Œº[f j | ‚Ñ±.le i]`. -/
def Submartingale [LE E] (f : Œπ ‚Üí Œ© ‚Üí E) (‚Ñ± : Filtration Œπ m0) (Œº : Measure Œ©) : Prop :=
  Adapted ‚Ñ± f ‚àß (‚àÄ i j, i ‚â§ j ‚Üí f i ‚â§·µê[Œº] Œº[f j|‚Ñ± i]) ‚àß ‚àÄ i, Integrable (f i) Œº

theorem martingale_const (‚Ñ± : Filtration Œπ m0) (Œº : Measure Œ©) [IsFiniteMeasure Œº] (x : E) :
    Martingale (fun _ _ => x) ‚Ñ± Œº :=
  ‚ü®adapted_const ‚Ñ± _, fun i j _ => by rw [condExp_const (‚Ñ±.le _)]‚ü©

theorem martingale_const_fun [OrderBot Œπ] (‚Ñ± : Filtration Œπ m0) (Œº : Measure Œ©)
    [SigmaFiniteFiltration Œº ‚Ñ±]
    {f : Œ© ‚Üí E} (hf : StronglyMeasurable[‚Ñ± ‚ä•] f) (hfint : Integrable f Œº) :
    Martingale (fun _ => f) ‚Ñ± Œº := by
  refine ‚ü®fun i => hf.mono <| ‚Ñ±.mono bot_le, fun i j _ => ?_‚ü©
  rw [condExp_of_stronglyMeasurable (‚Ñ±.le _) (hf.mono <| ‚Ñ±.mono bot_le) hfint]

variable (E) in
theorem martingale_zero (‚Ñ± : Filtration Œπ m0) (Œº : Measure Œ©) : Martingale (0 : Œπ ‚Üí Œ© ‚Üí E) ‚Ñ± Œº :=
  ‚ü®adapted_zero E ‚Ñ±, fun i j _ => by simp‚ü©

namespace Martingale

protected theorem adapted (hf : Martingale f ‚Ñ± Œº) : Adapted ‚Ñ± f :=
  hf.1

protected theorem stronglyMeasurable (hf : Martingale f ‚Ñ± Œº) (i : Œπ) :
    StronglyMeasurable[‚Ñ± i] (f i) :=
  hf.adapted i

theorem condExp_ae_eq (hf : Martingale f ‚Ñ± Œº) {i j : Œπ} (hij : i ‚â§ j) : Œº[f j|‚Ñ± i] =·µê[Œº] f i :=
  hf.2 i j hij

protected theorem integrable (hf : Martingale f ‚Ñ± Œº) (i : Œπ) : Integrable (f i) Œº :=
  integrable_condExp.congr (hf.condExp_ae_eq (le_refl i))

theorem setIntegral_eq [SigmaFiniteFiltration Œº ‚Ñ±] (hf : Martingale f ‚Ñ± Œº) {i j : Œπ} (hij : i ‚â§ j)
    {s : Set Œ©} (hs : MeasurableSet[‚Ñ± i] s) : ‚à´ œâ in s, f i œâ ‚àÇŒº = ‚à´ œâ in s, f j œâ ‚àÇŒº := by
  rw [‚Üê setIntegral_condExp (‚Ñ±.le i) (hf.integrable j) hs]
  refine setIntegral_congr_ae (‚Ñ±.le i s hs) ?_
  filter_upwards [hf.2 i j hij] with _ heq _ using heq.symm

theorem add (hf : Martingale f ‚Ñ± Œº) (hg : Martingale g ‚Ñ± Œº) : Martingale (f + g) ‚Ñ± Œº := by
  refine ‚ü®hf.adapted.add hg.adapted, fun i j hij => ?_‚ü©
  exact (condExp_add (hf.integrable j) (hg.integrable j) _).trans
    ((hf.2 i j hij).add (hg.2 i j hij))

theorem neg (hf : Martingale f ‚Ñ± Œº) : Martingale (-f) ‚Ñ± Œº :=
  ‚ü®hf.adapted.neg, fun i j hij => (condExp_neg ..).trans (hf.2 i j hij).neg‚ü©

theorem sub (hf : Martingale f ‚Ñ± Œº) (hg : Martingale g ‚Ñ± Œº) : Martingale (f - g) ‚Ñ± Œº := by
  rw [sub_eq_add_neg]; exact hf.add hg.neg

theorem smul (c : ‚Ñù) (hf : Martingale f ‚Ñ± Œº) : Martingale (c ‚Ä¢ f) ‚Ñ± Œº := by
  refine ‚ü®hf.adapted.smul c, fun i j hij => ?_‚ü©
  refine (condExp_smul ..).trans ((hf.2 i j hij).mono fun x hx => ?_)
  simp only [Pi.smul_apply, hx]

theorem supermartingale [Preorder E] (hf : Martingale f ‚Ñ± Œº) : Supermartingale f ‚Ñ± Œº :=
  ‚ü®hf.1, fun i j hij => (hf.2 i j hij).le, fun i => hf.integrable i‚ü©

theorem submartingale [Preorder E] (hf : Martingale f ‚Ñ± Œº) : Submartingale f ‚Ñ± Œº :=
  ‚ü®hf.1, fun i j hij => (hf.2 i j hij).symm.le, fun i => hf.integrable i‚ü©

end Martingale

theorem martingale_iff [PartialOrder E] :
    Martingale f ‚Ñ± Œº ‚Üî Supermartingale f ‚Ñ± Œº ‚àß Submartingale f ‚Ñ± Œº :=
  ‚ü®fun hf => ‚ü®hf.supermartingale, hf.submartingale‚ü©, fun ‚ü®hf‚ÇÅ, hf‚ÇÇ‚ü© =>
    ‚ü®hf‚ÇÅ.1, fun i j hij => (hf‚ÇÅ.2.1 i j hij).antisymm (hf‚ÇÇ.2.1 i j hij)‚ü©‚ü©

theorem martingale_condExp (f : Œ© ‚Üí E) (‚Ñ± : Filtration Œπ m0) (Œº : Measure Œ©)
    [SigmaFiniteFiltration Œº ‚Ñ±] : Martingale (fun i => Œº[f|‚Ñ± i]) ‚Ñ± Œº :=
  ‚ü®fun _ => stronglyMeasurable_condExp, fun _ j hij => condExp_condExp_of_le (‚Ñ±.mono hij) (‚Ñ±.le j)‚ü©

namespace Supermartingale

protected theorem adapted [LE E] (hf : Supermartingale f ‚Ñ± Œº) : Adapted ‚Ñ± f :=
  hf.1

protected theorem stronglyMeasurable [LE E] (hf : Supermartingale f ‚Ñ± Œº) (i : Œπ) :
    StronglyMeasurable[‚Ñ± i] (f i) :=
  hf.adapted i

protected theorem integrable [LE E] (hf : Supermartingale f ‚Ñ± Œº) (i : Œπ) : Integrable (f i) Œº :=
  hf.2.2 i

theorem condExp_ae_le [LE E] (hf : Supermartingale f ‚Ñ± Œº) {i j : Œπ} (hij : i ‚â§ j) :
    Œº[f j|‚Ñ± i] ‚â§·µê[Œº] f i :=
  hf.2.1 i j hij

theorem setIntegral_le [SigmaFiniteFiltration Œº ‚Ñ±] {f : Œπ ‚Üí Œ© ‚Üí ‚Ñù} (hf : Supermartingale f ‚Ñ± Œº)
    {i j : Œπ} (hij : i ‚â§ j) {s : Set Œ©} (hs : MeasurableSet[‚Ñ± i] s) :
    ‚à´ œâ in s, f j œâ ‚àÇŒº ‚â§ ‚à´ œâ in s, f i œâ ‚àÇŒº := by
  rw [‚Üê setIntegral_condExp (‚Ñ±.le i) (hf.integrable j) hs]
  refine setIntegral_mono_ae integrable_condExp.integrableOn (hf.integrable i).integrableOn ?_
  filter_upwards [hf.2.1 i j hij] with _ heq using heq

theorem add [Preorder E] [AddLeftMono E] (hf : Supermartingale f ‚Ñ± Œº)
    (hg : Supermartingale g ‚Ñ± Œº) : Supermartingale (f + g) ‚Ñ± Œº := by
  refine ‚ü®hf.1.add hg.1, fun i j hij => ?_, fun i => (hf.2.2 i).add (hg.2.2 i)‚ü©
  refine (condExp_add (hf.integrable j) (hg.integrable j) _).le.trans ?_
  filter_upwards [hf.2.1 i j hij, hg.2.1 i j hij]
  intros
  refine add_le_add ?_ ?_ <;> assumption

theorem add_martingale [Preorder E] [AddLeftMono E]
    (hf : Supermartingale f ‚Ñ± Œº) (hg : Martingale g ‚Ñ± Œº) : Supermartingale (f + g) ‚Ñ± Œº :=
  hf.add hg.supermartingale

theorem neg [Preorder E] [AddLeftMono E] (hf : Supermartingale f ‚Ñ± Œº) :
    Submartingale (-f) ‚Ñ± Œº := by
  refine ‚ü®hf.1.neg, fun i j hij => ?_, fun i => (hf.2.2 i).neg‚ü©
  refine EventuallyLE.trans ?_ (condExp_neg ..).symm.le
  filter_upwards [hf.2.1 i j hij] with _ _
  simpa

end Supermartingale

namespace Submartingale

protected theorem adapted [LE E] (hf : Submartingale f ‚Ñ± Œº) : Adapted ‚Ñ± f :=
  hf.1

protected theorem stronglyMeasurable [LE E] (hf : Submartingale f ‚Ñ± Œº) (i : Œπ) :
    StronglyMeasurable[‚Ñ± i] (f i) :=
  hf.adapted i

protected theorem integrable [LE E] (hf : Submartingale f ‚Ñ± Œº) (i : Œπ) : Integrable (f i) Œº :=
  hf.2.2 i

theorem ae_le_condExp [LE E] (hf : Submartingale f ‚Ñ± Œº) {i j : Œπ} (hij : i ‚â§ j) :
    f i ‚â§·µê[Œº] Œº[f j|‚Ñ± i] :=
  hf.2.1 i j hij

theorem add [Preorder E] [AddLeftMono E] (hf : Submartingale f ‚Ñ± Œº)
    (hg : Submartingale g ‚Ñ± Œº) : Submartingale (f + g) ‚Ñ± Œº := by
  refine ‚ü®hf.1.add hg.1, fun i j hij => ?_, fun i => (hf.2.2 i).add (hg.2.2 i)‚ü©
  refine EventuallyLE.trans ?_ (condExp_add (hf.integrable j) (hg.integrable j) _).symm.le
  filter_upwards [hf.2.1 i j hij, hg.2.1 i j hij]
  intros
  refine add_le_add ?_ ?_ <;> assumption

theorem add_martingale [Preorder E] [AddLeftMono E] (hf : Submartingale f ‚Ñ± Œº)
    (hg : Martingale g ‚Ñ± Œº) : Submartingale (f + g) ‚Ñ± Œº :=
  hf.add hg.submartingale

theorem neg [Preorder E] [AddLeftMono E] (hf : Submartingale f ‚Ñ± Œº) :
    Supermartingale (-f) ‚Ñ± Œº := by
  refine ‚ü®hf.1.neg, fun i j hij => (condExp_neg ..).le.trans ?_, fun i => (hf.2.2 i).neg‚ü©
  filter_upwards [hf.2.1 i j hij] with _ _
  simpa

/-- The converse of this lemma is `MeasureTheory.submartingale_of_setIntegral_le`. -/
theorem setIntegral_le [SigmaFiniteFiltration Œº ‚Ñ±] {f : Œπ ‚Üí Œ© ‚Üí ‚Ñù} (hf : Submartingale f ‚Ñ± Œº)
    {i j : Œπ} (hij : i ‚â§ j) {s : Set Œ©} (hs : MeasurableSet[‚Ñ± i] s) :
    ‚à´ œâ in s, f i œâ ‚àÇŒº ‚â§ ‚à´ œâ in s, f j œâ ‚àÇŒº := by
  rw [‚Üê neg_le_neg_iff, ‚Üê integral_neg, ‚Üê integral_neg]
  exact Supermartingale.setIntegral_le hf.neg hij hs

theorem sub_supermartingale [Preorder E] [AddLeftMono E]
    (hf : Submartingale f ‚Ñ± Œº) (hg : Supermartingale g ‚Ñ± Œº) : Submartingale (f - g) ‚Ñ± Œº := by
  rw [sub_eq_add_neg]; exact hf.add hg.neg

theorem sub_martingale [Preorder E] [AddLeftMono E] (hf : Submartingale f ‚Ñ± Œº)
    (hg : Martingale g ‚Ñ± Œº) : Submartingale (f - g) ‚Ñ± Œº :=
  hf.sub_supermartingale hg.supermartingale

protected theorem sup {f g : Œπ ‚Üí Œ© ‚Üí ‚Ñù} (hf : Submartingale f ‚Ñ± Œº) (hg : Submartingale g ‚Ñ± Œº) :
    Submartingale (f ‚äî g) ‚Ñ± Œº := by
  refine ‚ü®fun i => @StronglyMeasurable.sup _ _ _ _ (‚Ñ± i) _ _ _ (hf.adapted i) (hg.adapted i),
    fun i j hij => ?_, fun i => Integrable.sup (hf.integrable _) (hg.integrable _)‚ü©
  refine EventuallyLE.sup_le ?_ ?_
  ¬∑ exact EventuallyLE.trans (hf.2.1 i j hij)
      (condExp_mono (hf.integrable _) (Integrable.sup (hf.integrable j) (hg.integrable j))
        (Eventually.of_forall fun x => le_max_left _ _))
  ¬∑ exact EventuallyLE.trans (hg.2.1 i j hij)
      (condExp_mono (hg.integrable _) (Integrable.sup (hf.integrable j) (hg.integrable j))
        (Eventually.of_forall fun x => le_max_right _ _))

protected theorem pos {f : Œπ ‚Üí Œ© ‚Üí ‚Ñù} (hf : Submartingale f ‚Ñ± Œº) : Submartingale (f‚Å∫) ‚Ñ± Œº :=
  hf.sup (martingale_zero _ _ _).submartingale

end Submartingale

section Submartingale

theorem submartingale_of_setIntegral_le [SigmaFiniteFiltration Œº ‚Ñ±]
    {f : Œπ ‚Üí Œ© ‚Üí ‚Ñù} (hadp : Adapted ‚Ñ± f)
    (hint : ‚àÄ i, Integrable (f i) Œº) (hf : ‚àÄ i j : Œπ,
      i ‚â§ j ‚Üí ‚àÄ s : Set Œ©, MeasurableSet[‚Ñ± i] s ‚Üí ‚à´ œâ in s, f i œâ ‚àÇŒº ‚â§ ‚à´ œâ in s, f j œâ ‚àÇŒº) :
    Submartingale f ‚Ñ± Œº := by
  refine ‚ü®hadp, fun i j hij => ?_, hint‚ü©
  suffices f i ‚â§·µê[Œº.trim (‚Ñ±.le i)] Œº[f j|‚Ñ± i] by exact ae_le_of_ae_le_trim this
  suffices 0 ‚â§·µê[Œº.trim (‚Ñ±.le i)] Œº[f j|‚Ñ± i] - f i by
    filter_upwards [this] with x hx
    rwa [‚Üê sub_nonneg]
  refine ae_nonneg_of_forall_setIntegral_nonneg
    ((integrable_condExp.sub (hint i)).trim _ (stronglyMeasurable_condExp.sub <| hadp i))
      fun s hs _ => ?_
  specialize hf i j hij s hs
  rwa [‚Üê setIntegral_trim _ (stronglyMeasurable_condExp.sub <| hadp i) hs,
    integral_sub' integrable_condExp.integrableOn (hint i).integrableOn, sub_nonneg,
    setIntegral_condExp (‚Ñ±.le i) (hint j) hs]

theorem submartingale_of_condExp_sub_nonneg [SigmaFiniteFiltration Œº ‚Ñ±]
    {f : Œπ ‚Üí Œ© ‚Üí ‚Ñù} (hadp : Adapted ‚Ñ± f)
    (hint : ‚àÄ i, Integrable (f i) Œº) (hf : ‚àÄ i j, i ‚â§ j ‚Üí 0 ‚â§·µê[Œº] Œº[f j - f i|‚Ñ± i]) :
    Submartingale f ‚Ñ± Œº := by
  refine ‚ü®hadp, fun i j hij => ?_, hint‚ü©
  rw [‚Üê condExp_of_stronglyMeasurable (‚Ñ±.le _) (hadp _) (hint _), ‚Üê eventually_sub_nonneg]
  exact EventuallyLE.trans (hf i j hij) (condExp_sub (hint _) (hint _) _).le

theorem Submartingale.condExp_sub_nonneg {f : Œπ ‚Üí Œ© ‚Üí ‚Ñù} (hf : Submartingale f ‚Ñ± Œº) {i j : Œπ}
    (hij : i ‚â§ j) : 0 ‚â§·µê[Œº] Œº[f j - f i|‚Ñ± i] := by
  by_cases h : SigmaFinite (Œº.trim (‚Ñ±.le i))
  swap; ¬∑ rw [condExp_of_not_sigmaFinite (‚Ñ±.le i) h]
  refine EventuallyLE.trans ?_ (condExp_sub (hf.integrable _) (hf.integrable _) _).symm.le
  rw [eventually_sub_nonneg,
    condExp_of_stronglyMeasurable (‚Ñ±.le _) (hf.adapted _) (hf.integrable _)]
  exact hf.2.1 i j hij

theorem submartingale_iff_condExp_sub_nonneg [SigmaFiniteFiltration Œº ‚Ñ±] {f : Œπ ‚Üí Œ© ‚Üí ‚Ñù} :
    Submartingale f ‚Ñ± Œº ‚Üî
      Adapted ‚Ñ± f ‚àß (‚àÄ i, Integrable (f i) Œº) ‚àß ‚àÄ i j, i ‚â§ j ‚Üí 0 ‚â§·µê[Œº] Œº[f j - f i|‚Ñ± i] :=
  ‚ü®fun h => ‚ü®h.adapted, h.integrable, fun _ _ => h.condExp_sub_nonneg‚ü©, fun ‚ü®hadp, hint, h‚ü© =>
    submartingale_of_condExp_sub_nonneg hadp hint h‚ü©

end Submartingale

namespace Supermartingale

theorem sub_submartingale [Preorder E] [AddLeftMono E]
    (hf : Supermartingale f ‚Ñ± Œº) (hg : Submartingale g ‚Ñ± Œº) : Supermartingale (f - g) ‚Ñ± Œº := by
  rw [sub_eq_add_neg]; exact hf.add hg.neg

theorem sub_martingale [Preorder E] [AddLeftMono E]
    (hf : Supermartingale f ‚Ñ± Œº) (hg : Martingale g ‚Ñ± Œº) : Supermartingale (f - g) ‚Ñ± Œº :=
  hf.sub_submartingale hg.submartingale

section

variable {F : Type*} [NormedAddCommGroup F] [Lattice F] [NormedSpace ‚Ñù F] [CompleteSpace F]
  [IsOrderedModule ‚Ñù F]

theorem smul_nonneg {f : Œπ ‚Üí Œ© ‚Üí F} {c : ‚Ñù} (hc : 0 ‚â§ c) (hf : Supermartingale f ‚Ñ± Œº) :
    Supermartingale (c ‚Ä¢ f) ‚Ñ± Œº := by
  refine ‚ü®hf.1.smul c, fun i j hij => ?_, fun i => (hf.2.2 i).smul c‚ü©
  filter_upwards [condExp_smul c (f j) (‚Ñ± i), hf.2.1 i j hij] with œâ hœâ hle
  simpa only [hœâ, Pi.smul_apply] using smul_le_smul_of_nonneg_left hle hc

theorem smul_nonpos [IsOrderedAddMonoid F] {f : Œπ ‚Üí Œ© ‚Üí F} {c : ‚Ñù}
    (hc : c ‚â§ 0) (hf : Supermartingale f ‚Ñ± Œº) :
    Submartingale (c ‚Ä¢ f) ‚Ñ± Œº := by
  rw [‚Üê neg_neg c, neg_smul]
  exact (hf.smul_nonneg <| neg_nonneg.2 hc).neg

end

end Supermartingale

namespace Submartingale

section

variable {F : Type*} [NormedAddCommGroup F] [Lattice F] [IsOrderedAddMonoid F]
  [NormedSpace ‚Ñù F] [CompleteSpace F] [IsOrderedModule ‚Ñù F]

theorem smul_nonneg {f : Œπ ‚Üí Œ© ‚Üí F} {c : ‚Ñù} (hc : 0 ‚â§ c) (hf : Submartingale f ‚Ñ± Œº) :
    Submartingale (c ‚Ä¢ f) ‚Ñ± Œº := by
  rw [‚Üê neg_neg (c ‚Ä¢ f), ‚Üê smul_neg]
  exact Supermartingale.neg (hf.neg.smul_nonneg hc)

theorem smul_nonpos {f : Œπ ‚Üí Œ© ‚Üí F} {c : ‚Ñù} (hc : c ‚â§ 0) (hf : Submartingale f ‚Ñ± Œº) :
    Supermartingale (c ‚Ä¢ f) ‚Ñ± Œº := by
  rw [‚Üê neg_neg c, neg_smul]
  exact (hf.smul_nonneg <| neg_nonneg.2 hc).neg

end

end Submartingale

section Nat

variable {ùí¢ : Filtration ‚Ñï m0}

theorem submartingale_of_setIntegral_le_succ [IsFiniteMeasure Œº] {f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù}
    (hadp : Adapted ùí¢ f) (hint : ‚àÄ i, Integrable (f i) Œº)
    (hf : ‚àÄ i, ‚àÄ s : Set Œ©, MeasurableSet[ùí¢ i] s ‚Üí ‚à´ œâ in s, f i œâ ‚àÇŒº ‚â§ ‚à´ œâ in s, f (i + 1) œâ ‚àÇŒº) :
    Submartingale f ùí¢ Œº := by
  refine submartingale_of_setIntegral_le hadp hint fun i j hij s hs => ?_
  induction hij with
  | refl => rfl
  | step hk‚ÇÅ hk‚ÇÇ => exact hk‚ÇÇ.trans (hf _ s (ùí¢.mono hk‚ÇÅ _ hs))

theorem supermartingale_of_setIntegral_succ_le [IsFiniteMeasure Œº] {f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù}
    (hadp : Adapted ùí¢ f) (hint : ‚àÄ i, Integrable (f i) Œº)
    (hf : ‚àÄ i, ‚àÄ s : Set Œ©, MeasurableSet[ùí¢ i] s ‚Üí ‚à´ œâ in s, f (i + 1) œâ ‚àÇŒº ‚â§ ‚à´ œâ in s, f i œâ ‚àÇŒº) :
    Supermartingale f ùí¢ Œº := by
  rw [‚Üê neg_neg f]
  refine (submartingale_of_setIntegral_le_succ hadp.neg (fun i => (hint i).neg) ?_).neg
  simpa only [integral_neg, Pi.neg_apply, neg_le_neg_iff]

theorem martingale_of_setIntegral_eq_succ [IsFiniteMeasure Œº] {f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù} (hadp : Adapted ùí¢ f)
    (hint : ‚àÄ i, Integrable (f i) Œº)
    (hf : ‚àÄ i, ‚àÄ s : Set Œ©, MeasurableSet[ùí¢ i] s ‚Üí ‚à´ œâ in s, f i œâ ‚àÇŒº = ‚à´ œâ in s, f (i + 1) œâ ‚àÇŒº) :
    Martingale f ùí¢ Œº :=
  martingale_iff.2 ‚ü®supermartingale_of_setIntegral_succ_le hadp hint fun i s hs => (hf i s hs).ge,
    submartingale_of_setIntegral_le_succ hadp hint fun i s hs => (hf i s hs).le‚ü©

theorem submartingale_nat [IsFiniteMeasure Œº] {f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù} (hadp : Adapted ùí¢ f)
    (hint : ‚àÄ i, Integrable (f i) Œº) (hf : ‚àÄ i, f i ‚â§·µê[Œº] Œº[f (i + 1)|ùí¢ i]) :
    Submartingale f ùí¢ Œº := by
  refine submartingale_of_setIntegral_le_succ hadp hint fun i s hs => ?_
  have : ‚à´ œâ in s, f (i + 1) œâ ‚àÇŒº = ‚à´ œâ in s, (Œº[f (i + 1)|ùí¢ i]) œâ ‚àÇŒº :=
    (setIntegral_condExp (ùí¢.le i) (hint _) hs).symm
  rw [this]
  exact setIntegral_mono_ae (hint i).integrableOn integrable_condExp.integrableOn (hf i)

theorem supermartingale_nat [IsFiniteMeasure Œº] {f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù} (hadp : Adapted ùí¢ f)
    (hint : ‚àÄ i, Integrable (f i) Œº) (hf : ‚àÄ i, Œº[f (i + 1)|ùí¢ i] ‚â§·µê[Œº] f i) :
    Supermartingale f ùí¢ Œº := by
  rw [‚Üê neg_neg f]
  refine (submartingale_nat hadp.neg (fun i => (hint i).neg) fun i =>
    EventuallyLE.trans ?_ (condExp_neg ..).symm.le).neg
  filter_upwards [hf i] with x hx using neg_le_neg hx

theorem martingale_nat [IsFiniteMeasure Œº] {f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù} (hadp : Adapted ùí¢ f)
    (hint : ‚àÄ i, Integrable (f i) Œº) (hf : ‚àÄ i, f i =·µê[Œº] Œº[f (i + 1)|ùí¢ i]) : Martingale f ùí¢ Œº :=
  martingale_iff.2 ‚ü®supermartingale_nat hadp hint fun i => (hf i).symm.le,
    submartingale_nat hadp hint fun i => (hf i).le‚ü©

theorem submartingale_of_condExp_sub_nonneg_nat [IsFiniteMeasure Œº] {f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù}
    (hadp : Adapted ùí¢ f) (hint : ‚àÄ i, Integrable (f i) Œº)
    (hf : ‚àÄ i, 0 ‚â§·µê[Œº] Œº[f (i + 1) - f i|ùí¢ i]) : Submartingale f ùí¢ Œº := by
  refine submartingale_nat hadp hint fun i => ?_
  rw [‚Üê condExp_of_stronglyMeasurable (ùí¢.le _) (hadp _) (hint _), ‚Üê eventually_sub_nonneg]
  exact EventuallyLE.trans (hf i) (condExp_sub (hint _) (hint _) _).le

theorem supermartingale_of_condExp_sub_nonneg_nat [IsFiniteMeasure Œº] {f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù}
    (hadp : Adapted ùí¢ f) (hint : ‚àÄ i, Integrable (f i) Œº)
    (hf : ‚àÄ i, 0 ‚â§·µê[Œº] Œº[f i - f (i + 1)|ùí¢ i]) : Supermartingale f ùí¢ Œº := by
  rw [‚Üê neg_neg f]
  refine (submartingale_of_condExp_sub_nonneg_nat hadp.neg (fun i => (hint i).neg) ?_).neg
  simpa only [Pi.zero_apply, Pi.neg_apply, neg_sub_neg]

theorem martingale_of_condExp_sub_eq_zero_nat [IsFiniteMeasure Œº] {f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù}
    (hadp : Adapted ùí¢ f) (hint : ‚àÄ i, Integrable (f i) Œº)
    (hf : ‚àÄ i, Œº[f (i + 1) - f i|ùí¢ i] =·µê[Œº] 0) : Martingale f ùí¢ Œº := by
  refine martingale_iff.2 ‚ü®supermartingale_of_condExp_sub_nonneg_nat hadp hint fun i => ?_,
    submartingale_of_condExp_sub_nonneg_nat hadp hint fun i => (hf i).symm.le‚ü©
  rw [‚Üê neg_sub]
  refine (EventuallyEq.trans ?_ (condExp_neg ..).symm).le
  filter_upwards [hf i] with x hx
  simpa only [Pi.zero_apply, Pi.neg_apply, zero_eq_neg]

-- Note that one cannot use `Submartingale.zero_le_of_predictable` to prove the other two
-- corresponding lemmas without imposing more restrictions to the ordering of `E`
/-- A predictable submartingale is a.e. greater equal than its initial state. -/
theorem Submartingale.zero_le_of_predictable [Preorder E] [SigmaFiniteFiltration Œº ùí¢]
    {f : ‚Ñï ‚Üí Œ© ‚Üí E} (hfmgle : Submartingale f ùí¢ Œº) (hfadp : Adapted ùí¢ fun n => f (n + 1)) (n : ‚Ñï) :
    f 0 ‚â§·µê[Œº] f n := by
  induction n with
  | zero => rfl
  | succ k ih =>
    exact ih.trans ((hfmgle.2.1 k (k + 1) k.le_succ).trans_eq <| Germ.coe_eq.mp <|
    congr_arg Germ.ofFun <| condExp_of_stronglyMeasurable (ùí¢.le _) (hfadp _) <| hfmgle.integrable _)

/-- A predictable supermartingale is a.e. less equal than its initial state. -/
theorem Supermartingale.le_zero_of_predictable [Preorder E] [SigmaFiniteFiltration Œº ùí¢]
    {f : ‚Ñï ‚Üí Œ© ‚Üí E} (hfmgle : Supermartingale f ùí¢ Œº) (hfadp : Adapted ùí¢ fun n => f (n + 1))
    (n : ‚Ñï) : f n ‚â§·µê[Œº] f 0 := by
  induction n with
  | zero => rfl
  | succ k ih =>
    exact ((Germ.coe_eq.mp <| congr_arg Germ.ofFun <| condExp_of_stronglyMeasurable (ùí¢.le _)
      (hfadp _) <| hfmgle.integrable _).symm.trans_le (hfmgle.2.1 k (k + 1) k.le_succ)).trans ih

/-- A predictable martingale is a.e. equal to its initial state. -/
theorem Martingale.eq_zero_of_predictable [SigmaFiniteFiltration Œº ùí¢] {f : ‚Ñï ‚Üí Œ© ‚Üí E}
    (hfmgle : Martingale f ùí¢ Œº) (hfadp : Adapted ùí¢ fun n => f (n + 1)) (n : ‚Ñï) : f n =·µê[Œº] f 0 := by
  induction n with
  | zero => rfl
  | succ k ih =>
    exact ((Germ.coe_eq.mp (congr_arg Germ.ofFun <| condExp_of_stronglyMeasurable (ùí¢.le _) (hfadp _)
      (hfmgle.integrable _))).symm.trans (hfmgle.2 k (k + 1) k.le_succ)).trans ih

section

variable [MeasurableSpace E] [BorelSpace E] [SecondCountableTopology E]

/-- A predictable submartingale is a.e. greater equal than its initial state. -/
theorem Submartingale.zero_le_of_predictable' [Preorder E] [SigmaFiniteFiltration Œº ùí¢]
    {f : ‚Ñï ‚Üí Œ© ‚Üí E} (hfmgle : Submartingale f ùí¢ Œº) (hfadp : IsPredictable ùí¢ f) (n : ‚Ñï) :
    f 0 ‚â§·µê[Œº] f n := by
  induction n with
  | zero => rfl
  | succ k ih =>
    exact ih.trans ((hfmgle.2.1 k (k + 1) k.le_succ).trans_eq <| Germ.coe_eq.mp <|
      congr_arg Germ.ofFun <| condExp_of_stronglyMeasurable (ùí¢.le _)
      (hfadp.measurable_add_one _).stronglyMeasurable <| hfmgle.integrable _)

/-- A predictable supermartingale is a.e. less equal than its initial state. -/
theorem Supermartingale.le_zero_of_predictable' [Preorder E] [SigmaFiniteFiltration Œº ùí¢]
    {f : ‚Ñï ‚Üí Œ© ‚Üí E} (hfmgle : Supermartingale f ùí¢ Œº) (hfadp : IsPredictable ùí¢ f)
    (n : ‚Ñï) : f n ‚â§·µê[Œº] f 0 := by
  induction n with
  | zero => rfl
  | succ k ih =>
    exact ((Germ.coe_eq.mp <| congr_arg Germ.ofFun <| condExp_of_stronglyMeasurable (ùí¢.le _)
      (hfadp.measurable_add_one _).stronglyMeasurable <|
        hfmgle.integrable _).symm.trans_le (hfmgle.2.1 k (k + 1) k.le_succ)).trans ih

/-- A predictable martingale is a.e. equal to its initial state. -/
theorem Martingale.eq_zero_of_predictable' [SigmaFiniteFiltration Œº ùí¢] {f : ‚Ñï ‚Üí Œ© ‚Üí E}
    (hfmgle : Martingale f ùí¢ Œº) (hfadp : IsPredictable ùí¢ f) (n : ‚Ñï) : f n =·µê[Œº] f 0 := by
  induction n with
  | zero => rfl
  | succ k ih =>
    exact ((Germ.coe_eq.mp (congr_arg Germ.ofFun <| condExp_of_stronglyMeasurable (ùí¢.le _)
      (hfadp.measurable_add_one _).stronglyMeasurable
      (hfmgle.integrable _))).symm.trans (hfmgle.2 k (k + 1) k.le_succ)).trans ih

end
namespace Submartingale

protected theorem integrable_stoppedValue [LE E] {f : ‚Ñï ‚Üí Œ© ‚Üí E} (hf : Submartingale f ùí¢ Œº)
    {œÑ : Œ© ‚Üí ‚Ñï‚àû} (hœÑ : IsStoppingTime ùí¢ œÑ) {N : ‚Ñï} (hbdd : ‚àÄ œâ, œÑ œâ ‚â§ N) :
    Integrable (stoppedValue f œÑ) Œº :=
  integrable_stoppedValue ‚Ñï hœÑ hf.integrable hbdd

end Submartingale

theorem Submartingale.sum_mul_sub [IsFiniteMeasure Œº] {R : ‚Ñù} {Œæ f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù}
    (hf : Submartingale f ùí¢ Œº) (hŒæ : Adapted ùí¢ Œæ) (hbdd : ‚àÄ n œâ, Œæ n œâ ‚â§ R)
    (hnonneg : ‚àÄ n œâ, 0 ‚â§ Œæ n œâ) :
    Submartingale (fun n => ‚àë k ‚àà Finset.range n, Œæ k * (f (k + 1) - f k)) ùí¢ Œº := by
  have hŒæbdd : ‚àÄ i, ‚àÉ C, ‚àÄ œâ, |Œæ i œâ| ‚â§ C := fun i =>
    ‚ü®R, fun œâ => (abs_of_nonneg (hnonneg i œâ)).trans_le (hbdd i œâ)‚ü©
  have hint : ‚àÄ m, Integrable (‚àë k ‚àà Finset.range m, Œæ k * (f (k + 1) - f k)) Œº := fun m =>
    integrable_finset_sum' _ fun i _ => Integrable.bdd_mul ((hf.integrable _).sub (hf.integrable _))
      hŒæ.stronglyMeasurable.aestronglyMeasurable (hŒæbdd _)
  have hadp : Adapted ùí¢ fun n => ‚àë k ‚àà Finset.range n, Œæ k * (f (k + 1) - f k) := by
    intro m
    refine Finset.stronglyMeasurable_sum _ fun i hi => ?_
    rw [Finset.mem_range] at hi
    exact (hŒæ.stronglyMeasurable_le hi.le).mul
      ((hf.adapted.stronglyMeasurable_le (Nat.succ_le_of_lt hi)).sub
        (hf.adapted.stronglyMeasurable_le hi.le))
  refine submartingale_of_condExp_sub_nonneg_nat hadp hint fun i => ?_
  simp only [‚Üê Finset.sum_Ico_eq_sub _ (Nat.le_succ _),
    Nat.Ico_succ_singleton, Finset.sum_singleton]
  exact EventuallyLE.trans (EventuallyLE.mul_nonneg (Eventually.of_forall (hnonneg _))
    (hf.condExp_sub_nonneg (Nat.le_succ _))) (condExp_mul_of_stronglyMeasurable_left (hŒæ _)
    (((hf.integrable _).sub (hf.integrable _)).bdd_mul
      hŒæ.stronglyMeasurable.aestronglyMeasurable (hŒæbdd _))
    ((hf.integrable _).sub (hf.integrable _))).symm.le

/-- Given a discrete submartingale `f` and a predictable process `Œæ` (i.e. `Œæ (n + 1)` is adapted)
the process defined by `fun n => ‚àë k ‚àà Finset.range n, Œæ (k + 1) * (f (k + 1) - f k)` is also a
submartingale. -/
theorem Submartingale.sum_mul_sub' [IsFiniteMeasure Œº] {R : ‚Ñù} {Œæ f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù}
    (hf : Submartingale f ùí¢ Œº) (hŒæ : Adapted ùí¢ fun n => Œæ (n + 1)) (hbdd : ‚àÄ n œâ, Œæ n œâ ‚â§ R)
    (hnonneg : ‚àÄ n œâ, 0 ‚â§ Œæ n œâ) :
    Submartingale (fun n => ‚àë k ‚àà Finset.range n, Œæ (k + 1) * (f (k + 1) - f k)) ùí¢ Œº :=
  hf.sum_mul_sub hŒæ (fun _ => hbdd _) fun _ => hnonneg _

end Nat

end MeasureTheory
