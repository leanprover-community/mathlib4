/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Kexing Ying
-/
import Mathlib.Probability.Notation
import Mathlib.Probability.Process.Stopping

/-!
# Martingales

A family of functions `f : Î¹ â†’ Î© â†’ E` is a martingale with respect to a filtration `â„±` if every
`f i` is integrable, `f` is adapted with respect to `â„±` and for all `i â‰¤ j`,
`Î¼[f j | â„± i] =áµ[Î¼] f i`. On the other hand, `f : Î¹ â†’ Î© â†’ E` is said to be a supermartingale
with respect to the filtration `â„±` if `f i` is integrable, `f` is adapted with resepct to `â„±`
and for all `i â‰¤ j`, `Î¼[f j | â„± i] â‰¤áµ[Î¼] f i`. Finally, `f : Î¹ â†’ Î© â†’ E` is said to be a
submartingale with respect to the filtration `â„±` if `f i` is integrable, `f` is adapted with
resepct to `â„±` and for all `i â‰¤ j`, `f i â‰¤áµ[Î¼] Î¼[f j | â„± i]`.

The definitions of filtration and adapted can be found in `Probability.Process.Stopping`.

### Definitions

* `MeasureTheory.Martingale f â„± Î¼`: `f` is a martingale with respect to filtration `â„±` and
  measure `Î¼`.
* `MeasureTheory.Supermartingale f â„± Î¼`: `f` is a supermartingale with respect to
  filtration `â„±` and measure `Î¼`.
* `MeasureTheory.Submartingale f â„± Î¼`: `f` is a submartingale with respect to filtration `â„±` and
  measure `Î¼`.

### Results

* `MeasureTheory.martingale_condexp f â„± Î¼`: the sequence `fun i â†¦ Î¼[f | â„± i, â„±.le i])` is a
  martingale with respect to `â„±` and `Î¼`.

-/


open TopologicalSpace Filter

open scoped NNReal ENNReal MeasureTheory ProbabilityTheory

namespace MeasureTheory

variable {Î© E Î¹ : Type*} [Preorder Î¹] {m0 : MeasurableSpace Î©} {Î¼ : Measure Î©}
  [NormedAddCommGroup E] [NormedSpace â„ E] [CompleteSpace E] {f g : Î¹ â†’ Î© â†’ E} {â„± : Filtration Î¹ m0}

/-- A family of functions `f : Î¹ â†’ Î© â†’ E` is a martingale with respect to a filtration `â„±` if `f`
is adapted with respect to `â„±` and for all `i â‰¤ j`, `Î¼[f j | â„± i] =áµ[Î¼] f i`. -/
def Martingale (f : Î¹ â†’ Î© â†’ E) (â„± : Filtration Î¹ m0) (Î¼ : Measure Î©) : Prop :=
  Adapted â„± f âˆ§ âˆ€ i j, i â‰¤ j â†’ Î¼[f j|â„± i] =áµ[Î¼] f i

/-- A family of integrable functions `f : Î¹ â†’ Î© â†’ E` is a supermartingale with respect to a
filtration `â„±` if `f` is adapted with respect to `â„±` and for all `i â‰¤ j`,
`Î¼[f j | â„±.le i] â‰¤áµ[Î¼] f i`. -/
def Supermartingale [LE E] (f : Î¹ â†’ Î© â†’ E) (â„± : Filtration Î¹ m0) (Î¼ : Measure Î©) : Prop :=
  Adapted â„± f âˆ§ (âˆ€ i j, i â‰¤ j â†’ Î¼[f j|â„± i] â‰¤áµ[Î¼] f i) âˆ§ âˆ€ i, Integrable (f i) Î¼

/-- A family of integrable functions `f : Î¹ â†’ Î© â†’ E` is a submartingale with respect to a
filtration `â„±` if `f` is adapted with respect to `â„±` and for all `i â‰¤ j`,
`f i â‰¤áµ[Î¼] Î¼[f j | â„±.le i]`. -/
def Submartingale [LE E] (f : Î¹ â†’ Î© â†’ E) (â„± : Filtration Î¹ m0) (Î¼ : Measure Î©) : Prop :=
  Adapted â„± f âˆ§ (âˆ€ i j, i â‰¤ j â†’ f i â‰¤áµ[Î¼] Î¼[f j|â„± i]) âˆ§ âˆ€ i, Integrable (f i) Î¼

theorem martingale_const (â„± : Filtration Î¹ m0) (Î¼ : Measure Î©) [IsFiniteMeasure Î¼] (x : E) :
    Martingale (fun _ _ => x) â„± Î¼ :=
  âŸ¨adapted_const â„± _, fun i j _ => by rw [condexp_const (â„±.le _)]âŸ©

theorem martingale_const_fun [OrderBot Î¹] (â„± : Filtration Î¹ m0) (Î¼ : Measure Î©) [IsFiniteMeasure Î¼]
    {f : Î© â†’ E} (hf : StronglyMeasurable[â„± âŠ¥] f) (hfint : Integrable f Î¼) :
    Martingale (fun _ => f) â„± Î¼ := by
  refine âŸ¨fun i â†¦ hf.mono <| â„±.mono bot_le, fun i j _ => ?_âŸ©
  rw [condexp_of_stronglyMeasurable (â„±.le _) (hf.mono <| â„±.mono bot_le) hfint]

variable (E)

theorem martingale_zero (â„± : Filtration Î¹ m0) (Î¼ : Measure Î©) : Martingale (0 : Î¹ â†’ Î© â†’ E) â„± Î¼ :=
  âŸ¨adapted_zero E â„±, fun i j _ => by rw [Pi.zero_apply, condexp_zero]; simpâŸ©

variable {E}

namespace Martingale

protected theorem adapted (hf : Martingale f â„± Î¼) : Adapted â„± f :=
  hf.1

protected theorem stronglyMeasurable (hf : Martingale f â„± Î¼) (i : Î¹) :
    StronglyMeasurable[â„± i] (f i) :=
  hf.adapted i

theorem condexp_ae_eq (hf : Martingale f â„± Î¼) {i j : Î¹} (hij : i â‰¤ j) : Î¼[f j|â„± i] =áµ[Î¼] f i :=
  hf.2 i j hij

protected theorem integrable (hf : Martingale f â„± Î¼) (i : Î¹) : Integrable (f i) Î¼ :=
  integrable_condexp.congr (hf.condexp_ae_eq (le_refl i))

theorem setIntegral_eq [SigmaFiniteFiltration Î¼ â„±] (hf : Martingale f â„± Î¼) {i j : Î¹} (hij : i â‰¤ j)
    {s : Set Î©} (hs : MeasurableSet[â„± i] s) : âˆ« Ï‰ in s, f i Ï‰ âˆ‚Î¼ = âˆ« Ï‰ in s, f j Ï‰ âˆ‚Î¼ := by
  rw [â† @setIntegral_condexp _ _ _ _ _ (â„± i) m0 _ _ _ (â„±.le i) _ (hf.integrable j) hs]
  refine setIntegral_congr_ae (â„±.le i s hs) ?_
  filter_upwards [hf.2 i j hij] with _ heq _ using heq.symm

@[deprecated (since := "2024-04-17")]
alias set_integral_eq := setIntegral_eq

theorem add (hf : Martingale f â„± Î¼) (hg : Martingale g â„± Î¼) : Martingale (f + g) â„± Î¼ := by
  refine âŸ¨hf.adapted.add hg.adapted, fun i j hij => ?_âŸ©
  exact (condexp_add (hf.integrable j) (hg.integrable j)).trans ((hf.2 i j hij).add (hg.2 i j hij))

theorem neg (hf : Martingale f â„± Î¼) : Martingale (-f) â„± Î¼ :=
  âŸ¨hf.adapted.neg, fun i j hij => (condexp_neg (f j)).trans (hf.2 i j hij).negâŸ©

theorem sub (hf : Martingale f â„± Î¼) (hg : Martingale g â„± Î¼) : Martingale (f - g) â„± Î¼ := by
  rw [sub_eq_add_neg]; exact hf.add hg.neg

theorem smul (c : â„) (hf : Martingale f â„± Î¼) : Martingale (c â€¢ f) â„± Î¼ := by
  refine âŸ¨hf.adapted.smul c, fun i j hij => ?_âŸ©
  refine (condexp_smul c (f j)).trans ((hf.2 i j hij).mono fun x hx => ?_)
  simp only [Pi.smul_apply, hx]

theorem supermartingale [Preorder E] (hf : Martingale f â„± Î¼) : Supermartingale f â„± Î¼ :=
  âŸ¨hf.1, fun i j hij => (hf.2 i j hij).le, fun i â†¦ hf.integrable iâŸ©

theorem submartingale [Preorder E] (hf : Martingale f â„± Î¼) : Submartingale f â„± Î¼ :=
  âŸ¨hf.1, fun i j hij => (hf.2 i j hij).symm.le, fun i â†¦ hf.integrable iâŸ©

end Martingale

theorem martingale_iff [PartialOrder E] :
    Martingale f â„± Î¼ â†” Supermartingale f â„± Î¼ âˆ§ Submartingale f â„± Î¼ :=
  âŸ¨fun hf => âŸ¨hf.supermartingale, hf.submartingaleâŸ©, fun âŸ¨hfâ‚, hfâ‚‚âŸ© =>
    âŸ¨hfâ‚.1, fun i j hij => (hfâ‚.2.1 i j hij).antisymm (hfâ‚‚.2.1 i j hij)âŸ©âŸ©

theorem martingale_condexp (f : Î© â†’ E) (â„± : Filtration Î¹ m0) (Î¼ : Measure Î©)
    [SigmaFiniteFiltration Î¼ â„±] : Martingale (fun i â†¦ Î¼[f|â„± i]) â„± Î¼ :=
  âŸ¨fun _ => stronglyMeasurable_condexp, fun _ j hij => condexp_condexp_of_le (â„±.mono hij) (â„±.le j)âŸ©

namespace Supermartingale

protected theorem adapted [LE E] (hf : Supermartingale f â„± Î¼) : Adapted â„± f :=
  hf.1

protected theorem stronglyMeasurable [LE E] (hf : Supermartingale f â„± Î¼) (i : Î¹) :
    StronglyMeasurable[â„± i] (f i) :=
  hf.adapted i

protected theorem integrable [LE E] (hf : Supermartingale f â„± Î¼) (i : Î¹) : Integrable (f i) Î¼ :=
  hf.2.2 i

theorem condexp_ae_le [LE E] (hf : Supermartingale f â„± Î¼) {i j : Î¹} (hij : i â‰¤ j) :
    Î¼[f j|â„± i] â‰¤áµ[Î¼] f i :=
  hf.2.1 i j hij

theorem setIntegral_le [SigmaFiniteFiltration Î¼ â„±] {f : Î¹ â†’ Î© â†’ â„} (hf : Supermartingale f â„± Î¼)
    {i j : Î¹} (hij : i â‰¤ j) {s : Set Î©} (hs : MeasurableSet[â„± i] s) :
    âˆ« Ï‰ in s, f j Ï‰ âˆ‚Î¼ â‰¤ âˆ« Ï‰ in s, f i Ï‰ âˆ‚Î¼ := by
  rw [â† setIntegral_condexp (â„±.le i) (hf.integrable j) hs]
  refine setIntegral_mono_ae integrable_condexp.integrableOn (hf.integrable i).integrableOn ?_
  filter_upwards [hf.2.1 i j hij] with _ heq using heq

@[deprecated (since := "2024-04-17")]
alias set_integral_le := setIntegral_le

theorem add [Preorder E] [AddLeftMono E] (hf : Supermartingale f â„± Î¼)
    (hg : Supermartingale g â„± Î¼) : Supermartingale (f + g) â„± Î¼ := by
  refine âŸ¨hf.1.add hg.1, fun i j hij => ?_, fun i â†¦ (hf.2.2 i).add (hg.2.2 i)âŸ©
  refine (condexp_add (hf.integrable j) (hg.integrable j)).le.trans ?_
  filter_upwards [hf.2.1 i j hij, hg.2.1 i j hij]
  intros
  refine add_le_add ?_ ?_ <;> assumption

theorem add_martingale [Preorder E] [AddLeftMono E]
    (hf : Supermartingale f â„± Î¼) (hg : Martingale g â„± Î¼) : Supermartingale (f + g) â„± Î¼ :=
  hf.add hg.supermartingale

theorem neg [Preorder E] [AddLeftMono E] (hf : Supermartingale f â„± Î¼) :
    Submartingale (-f) â„± Î¼ := by
  refine âŸ¨hf.1.neg, fun i j hij => ?_, fun i â†¦ (hf.2.2 i).negâŸ©
  refine EventuallyLE.trans ?_ (condexp_neg (f j)).symm.le
  filter_upwards [hf.2.1 i j hij] with _ _
  simpa

end Supermartingale

namespace Submartingale

protected theorem adapted [LE E] (hf : Submartingale f â„± Î¼) : Adapted â„± f :=
  hf.1

protected theorem stronglyMeasurable [LE E] (hf : Submartingale f â„± Î¼) (i : Î¹) :
    StronglyMeasurable[â„± i] (f i) :=
  hf.adapted i

protected theorem integrable [LE E] (hf : Submartingale f â„± Î¼) (i : Î¹) : Integrable (f i) Î¼ :=
  hf.2.2 i

theorem ae_le_condexp [LE E] (hf : Submartingale f â„± Î¼) {i j : Î¹} (hij : i â‰¤ j) :
    f i â‰¤áµ[Î¼] Î¼[f j|â„± i] :=
  hf.2.1 i j hij

theorem add [Preorder E] [AddLeftMono E] (hf : Submartingale f â„± Î¼)
    (hg : Submartingale g â„± Î¼) : Submartingale (f + g) â„± Î¼ := by
  refine âŸ¨hf.1.add hg.1, fun i j hij => ?_, fun i â†¦ (hf.2.2 i).add (hg.2.2 i)âŸ©
  refine EventuallyLE.trans ?_ (condexp_add (hf.integrable j) (hg.integrable j)).symm.le
  filter_upwards [hf.2.1 i j hij, hg.2.1 i j hij]
  intros
  refine add_le_add ?_ ?_ <;> assumption

theorem add_martingale [Preorder E] [AddLeftMono E] (hf : Submartingale f â„± Î¼)
    (hg : Martingale g â„± Î¼) : Submartingale (f + g) â„± Î¼ :=
  hf.add hg.submartingale

theorem neg [Preorder E] [AddLeftMono E] (hf : Submartingale f â„± Î¼) :
    Supermartingale (-f) â„± Î¼ := by
  refine âŸ¨hf.1.neg, fun i j hij => (condexp_neg (f j)).le.trans ?_, fun i â†¦ (hf.2.2 i).negâŸ©
  filter_upwards [hf.2.1 i j hij] with _ _
  simpa

/-- The converse of this lemma is `MeasureTheory.submartingale_of_setIntegral_le`. -/
theorem setIntegral_le [SigmaFiniteFiltration Î¼ â„±] {f : Î¹ â†’ Î© â†’ â„} (hf : Submartingale f â„± Î¼)
    {i j : Î¹} (hij : i â‰¤ j) {s : Set Î©} (hs : MeasurableSet[â„± i] s) :
    âˆ« Ï‰ in s, f i Ï‰ âˆ‚Î¼ â‰¤ âˆ« Ï‰ in s, f j Ï‰ âˆ‚Î¼ := by
  rw [â† neg_le_neg_iff, â† integral_neg, â† integral_neg]
  exact Supermartingale.setIntegral_le hf.neg hij hs

@[deprecated (since := "2024-04-17")]
alias set_integral_le := setIntegral_le

theorem sub_supermartingale [Preorder E] [AddLeftMono E]
    (hf : Submartingale f â„± Î¼) (hg : Supermartingale g â„± Î¼) : Submartingale (f - g) â„± Î¼ := by
  rw [sub_eq_add_neg]; exact hf.add hg.neg

theorem sub_martingale [Preorder E] [AddLeftMono E] (hf : Submartingale f â„± Î¼)
    (hg : Martingale g â„± Î¼) : Submartingale (f - g) â„± Î¼ :=
  hf.sub_supermartingale hg.supermartingale

protected theorem sup {f g : Î¹ â†’ Î© â†’ â„} (hf : Submartingale f â„± Î¼) (hg : Submartingale g â„± Î¼) :
    Submartingale (f âŠ” g) â„± Î¼ := by
  refine âŸ¨fun i â†¦ @StronglyMeasurable.sup _ _ _ _ (â„± i) _ _ _ (hf.adapted i) (hg.adapted i),
    fun i j hij => ?_, fun i â†¦ Integrable.sup (hf.integrable _) (hg.integrable _)âŸ©
  refine EventuallyLE.sup_le ?_ ?_
  Â· exact EventuallyLE.trans (hf.2.1 i j hij)
      (condexp_mono (hf.integrable _) (Integrable.sup (hf.integrable j) (hg.integrable j))
        (Eventually.of_forall fun x â†¦ le_max_left _ _))
  Â· exact EventuallyLE.trans (hg.2.1 i j hij)
      (condexp_mono (hg.integrable _) (Integrable.sup (hf.integrable j) (hg.integrable j))
        (Eventually.of_forall fun x â†¦ le_max_right _ _))

protected theorem pos {f : Î¹ â†’ Î© â†’ â„} (hf : Submartingale f â„± Î¼) : Submartingale (fâº) â„± Î¼ :=
  hf.sup (martingale_zero _ _ _).submartingale

end Submartingale

section Submartingale

theorem submartingale_of_setIntegral_le [IsFiniteMeasure Î¼] {f : Î¹ â†’ Î© â†’ â„} (hadp : Adapted â„± f)
    (hint : âˆ€ i, Integrable (f i) Î¼) (hf : âˆ€ i j : Î¹,
      i â‰¤ j â†’ âˆ€ s : Set Î©, MeasurableSet[â„± i] s â†’ âˆ« Ï‰ in s, f i Ï‰ âˆ‚Î¼ â‰¤ âˆ« Ï‰ in s, f j Ï‰ âˆ‚Î¼) :
    Submartingale f â„± Î¼ := by
  refine âŸ¨hadp, fun i j hij => ?_, hintâŸ©
  suffices f i â‰¤áµ[Î¼.trim (â„±.le i)] Î¼[f j|â„± i] by exact ae_le_of_ae_le_trim this
  suffices 0 â‰¤áµ[Î¼.trim (â„±.le i)] Î¼[f j|â„± i] - f i by
    filter_upwards [this] with x hx
    rwa [â† sub_nonneg]
  refine ae_nonneg_of_forall_setIntegral_nonneg
    ((integrable_condexp.sub (hint i)).trim _ (stronglyMeasurable_condexp.sub <| hadp i))
      fun s hs _ => ?_
  specialize hf i j hij s hs
  rwa [â† setIntegral_trim _ (stronglyMeasurable_condexp.sub <| hadp i) hs,
    integral_sub' integrable_condexp.integrableOn (hint i).integrableOn, sub_nonneg,
    setIntegral_condexp (â„±.le i) (hint j) hs]

@[deprecated (since := "2024-04-17")]
alias submartingale_of_set_integral_le := submartingale_of_setIntegral_le

theorem submartingale_of_condexp_sub_nonneg [IsFiniteMeasure Î¼] {f : Î¹ â†’ Î© â†’ â„} (hadp : Adapted â„± f)
    (hint : âˆ€ i, Integrable (f i) Î¼) (hf : âˆ€ i j, i â‰¤ j â†’ 0 â‰¤áµ[Î¼] Î¼[f j - f i|â„± i]) :
    Submartingale f â„± Î¼ := by
  refine âŸ¨hadp, fun i j hij => ?_, hintâŸ©
  rw [â† condexp_of_stronglyMeasurable (â„±.le _) (hadp _) (hint _), â† eventually_sub_nonneg]
  exact EventuallyLE.trans (hf i j hij) (condexp_sub (hint _) (hint _)).le

theorem Submartingale.condexp_sub_nonneg {f : Î¹ â†’ Î© â†’ â„} (hf : Submartingale f â„± Î¼) {i j : Î¹}
    (hij : i â‰¤ j) : 0 â‰¤áµ[Î¼] Î¼[f j - f i|â„± i] := by
  by_cases h : SigmaFinite (Î¼.trim (â„±.le i))
  swap; Â· rw [condexp_of_not_sigmaFinite (â„±.le i) h]
  refine EventuallyLE.trans ?_ (condexp_sub (hf.integrable _) (hf.integrable _)).symm.le
  rw [eventually_sub_nonneg,
    condexp_of_stronglyMeasurable (â„±.le _) (hf.adapted _) (hf.integrable _)]
  exact hf.2.1 i j hij

theorem submartingale_iff_condexp_sub_nonneg [IsFiniteMeasure Î¼] {f : Î¹ â†’ Î© â†’ â„} :
    Submartingale f â„± Î¼ â†”
      Adapted â„± f âˆ§ (âˆ€ i, Integrable (f i) Î¼) âˆ§ âˆ€ i j, i â‰¤ j â†’ 0 â‰¤áµ[Î¼] Î¼[f j - f i|â„± i] :=
  âŸ¨fun h â†¦ âŸ¨h.adapted, h.integrable, fun _ _ => h.condexp_sub_nonnegâŸ©, fun âŸ¨hadp, hint, hâŸ© =>
    submartingale_of_condexp_sub_nonneg hadp hint hâŸ©

end Submartingale

namespace Supermartingale

theorem sub_submartingale [Preorder E] [AddLeftMono E]
    (hf : Supermartingale f â„± Î¼) (hg : Submartingale g â„± Î¼) : Supermartingale (f - g) â„± Î¼ := by
  rw [sub_eq_add_neg]; exact hf.add hg.neg

theorem sub_martingale [Preorder E] [AddLeftMono E]
    (hf : Supermartingale f â„± Î¼) (hg : Martingale g â„± Î¼) : Supermartingale (f - g) â„± Î¼ :=
  hf.sub_submartingale hg.submartingale

section

variable {F : Type*} [NormedLatticeAddCommGroup F] [NormedSpace â„ F] [CompleteSpace F]
  [OrderedSMul â„ F]

theorem smul_nonneg {f : Î¹ â†’ Î© â†’ F} {c : â„} (hc : 0 â‰¤ c) (hf : Supermartingale f â„± Î¼) :
    Supermartingale (c â€¢ f) â„± Î¼ := by
  refine âŸ¨hf.1.smul c, fun i j hij => ?_, fun i â†¦ (hf.2.2 i).smul câŸ©
  refine (condexp_smul c (f j)).le.trans ?_
  filter_upwards [hf.2.1 i j hij] with _ hle
  simp_rw [Pi.smul_apply]
  exact smul_le_smul_of_nonneg_left hle hc

theorem smul_nonpos {f : Î¹ â†’ Î© â†’ F} {c : â„} (hc : c â‰¤ 0) (hf : Supermartingale f â„± Î¼) :
    Submartingale (c â€¢ f) â„± Î¼ := by
  rw [â† neg_neg c, (by ext (i x); simp : - -c â€¢ f = -(-c â€¢ f))]
  exact (hf.smul_nonneg <| neg_nonneg.2 hc).neg

end

end Supermartingale

namespace Submartingale

section

variable {F : Type*} [NormedLatticeAddCommGroup F] [NormedSpace â„ F] [CompleteSpace F]
  [OrderedSMul â„ F]

theorem smul_nonneg {f : Î¹ â†’ Î© â†’ F} {c : â„} (hc : 0 â‰¤ c) (hf : Submartingale f â„± Î¼) :
    Submartingale (c â€¢ f) â„± Î¼ := by
  rw [â† neg_neg c, (by ext (i x); simp : - -c â€¢ f = -(c â€¢ -f))]
  exact Supermartingale.neg (hf.neg.smul_nonneg hc)

theorem smul_nonpos {f : Î¹ â†’ Î© â†’ F} {c : â„} (hc : c â‰¤ 0) (hf : Submartingale f â„± Î¼) :
    Supermartingale (c â€¢ f) â„± Î¼ := by
  rw [â† neg_neg c, (by ext (i x); simp : - -c â€¢ f = -(-c â€¢ f))]
  exact (hf.smul_nonneg <| neg_nonneg.2 hc).neg

end

end Submartingale

section Nat

variable {ğ’¢ : Filtration â„• m0}

theorem submartingale_of_setIntegral_le_succ [IsFiniteMeasure Î¼] {f : â„• â†’ Î© â†’ â„}
    (hadp : Adapted ğ’¢ f) (hint : âˆ€ i, Integrable (f i) Î¼)
    (hf : âˆ€ i, âˆ€ s : Set Î©, MeasurableSet[ğ’¢ i] s â†’ âˆ« Ï‰ in s, f i Ï‰ âˆ‚Î¼ â‰¤ âˆ« Ï‰ in s, f (i + 1) Ï‰ âˆ‚Î¼) :
    Submartingale f ğ’¢ Î¼ := by
  refine submartingale_of_setIntegral_le hadp hint fun i j hij s hs => ?_
  induction' hij with k hkâ‚ hkâ‚‚
  Â· exact le_rfl
  Â· exact le_trans hkâ‚‚ (hf k s (ğ’¢.mono hkâ‚ _ hs))

@[deprecated (since := "2024-04-17")]
alias submartingale_of_set_integral_le_succ := submartingale_of_setIntegral_le_succ

theorem supermartingale_of_setIntegral_succ_le [IsFiniteMeasure Î¼] {f : â„• â†’ Î© â†’ â„}
    (hadp : Adapted ğ’¢ f) (hint : âˆ€ i, Integrable (f i) Î¼)
    (hf : âˆ€ i, âˆ€ s : Set Î©, MeasurableSet[ğ’¢ i] s â†’ âˆ« Ï‰ in s, f (i + 1) Ï‰ âˆ‚Î¼ â‰¤ âˆ« Ï‰ in s, f i Ï‰ âˆ‚Î¼) :
    Supermartingale f ğ’¢ Î¼ := by
  rw [â† neg_neg f]
  refine (submartingale_of_setIntegral_le_succ hadp.neg (fun i â†¦ (hint i).neg) ?_).neg
  simpa only [integral_neg, Pi.neg_apply, neg_le_neg_iff]

@[deprecated (since := "2024-04-17")]
alias supermartingale_of_set_integral_succ_le := supermartingale_of_setIntegral_succ_le

theorem martingale_of_setIntegral_eq_succ [IsFiniteMeasure Î¼] {f : â„• â†’ Î© â†’ â„} (hadp : Adapted ğ’¢ f)
    (hint : âˆ€ i, Integrable (f i) Î¼)
    (hf : âˆ€ i, âˆ€ s : Set Î©, MeasurableSet[ğ’¢ i] s â†’ âˆ« Ï‰ in s, f i Ï‰ âˆ‚Î¼ = âˆ« Ï‰ in s, f (i + 1) Ï‰ âˆ‚Î¼) :
    Martingale f ğ’¢ Î¼ :=
  martingale_iff.2 âŸ¨supermartingale_of_setIntegral_succ_le hadp hint fun i s hs => (hf i s hs).ge,
    submartingale_of_setIntegral_le_succ hadp hint fun i s hs => (hf i s hs).leâŸ©

@[deprecated (since := "2024-04-17")]
alias martingale_of_set_integral_eq_succ := martingale_of_setIntegral_eq_succ

theorem submartingale_nat [IsFiniteMeasure Î¼] {f : â„• â†’ Î© â†’ â„} (hadp : Adapted ğ’¢ f)
    (hint : âˆ€ i, Integrable (f i) Î¼) (hf : âˆ€ i, f i â‰¤áµ[Î¼] Î¼[f (i + 1)|ğ’¢ i]) :
    Submartingale f ğ’¢ Î¼ := by
  refine submartingale_of_setIntegral_le_succ hadp hint fun i s hs => ?_
  have : âˆ« Ï‰ in s, f (i + 1) Ï‰ âˆ‚Î¼ = âˆ« Ï‰ in s, (Î¼[f (i + 1)|ğ’¢ i]) Ï‰ âˆ‚Î¼ :=
    (setIntegral_condexp (ğ’¢.le i) (hint _) hs).symm
  rw [this]
  exact setIntegral_mono_ae (hint i).integrableOn integrable_condexp.integrableOn (hf i)

theorem supermartingale_nat [IsFiniteMeasure Î¼] {f : â„• â†’ Î© â†’ â„} (hadp : Adapted ğ’¢ f)
    (hint : âˆ€ i, Integrable (f i) Î¼) (hf : âˆ€ i, Î¼[f (i + 1)|ğ’¢ i] â‰¤áµ[Î¼] f i) :
    Supermartingale f ğ’¢ Î¼ := by
  rw [â† neg_neg f]
  refine (submartingale_nat hadp.neg (fun i â†¦ (hint i).neg) fun i =>
    EventuallyLE.trans ?_ (condexp_neg _).symm.le).neg
  filter_upwards [hf i] with x hx using neg_le_neg hx

theorem martingale_nat [IsFiniteMeasure Î¼] {f : â„• â†’ Î© â†’ â„} (hadp : Adapted ğ’¢ f)
    (hint : âˆ€ i, Integrable (f i) Î¼) (hf : âˆ€ i, f i =áµ[Î¼] Î¼[f (i + 1)|ğ’¢ i]) : Martingale f ğ’¢ Î¼ :=
  martingale_iff.2 âŸ¨supermartingale_nat hadp hint fun i â†¦ (hf i).symm.le,
    submartingale_nat hadp hint fun i â†¦ (hf i).leâŸ©

theorem submartingale_of_condexp_sub_nonneg_nat [IsFiniteMeasure Î¼] {f : â„• â†’ Î© â†’ â„}
    (hadp : Adapted ğ’¢ f) (hint : âˆ€ i, Integrable (f i) Î¼)
    (hf : âˆ€ i, 0 â‰¤áµ[Î¼] Î¼[f (i + 1) - f i|ğ’¢ i]) : Submartingale f ğ’¢ Î¼ := by
  refine submartingale_nat hadp hint fun i â†¦ ?_
  rw [â† condexp_of_stronglyMeasurable (ğ’¢.le _) (hadp _) (hint _), â† eventually_sub_nonneg]
  exact EventuallyLE.trans (hf i) (condexp_sub (hint _) (hint _)).le

theorem supermartingale_of_condexp_sub_nonneg_nat [IsFiniteMeasure Î¼] {f : â„• â†’ Î© â†’ â„}
    (hadp : Adapted ğ’¢ f) (hint : âˆ€ i, Integrable (f i) Î¼)
    (hf : âˆ€ i, 0 â‰¤áµ[Î¼] Î¼[f i - f (i + 1)|ğ’¢ i]) : Supermartingale f ğ’¢ Î¼ := by
  rw [â† neg_neg f]
  refine (submartingale_of_condexp_sub_nonneg_nat hadp.neg (fun i â†¦ (hint i).neg) ?_).neg
  simpa only [Pi.zero_apply, Pi.neg_apply, neg_sub_neg]

theorem martingale_of_condexp_sub_eq_zero_nat [IsFiniteMeasure Î¼] {f : â„• â†’ Î© â†’ â„}
    (hadp : Adapted ğ’¢ f) (hint : âˆ€ i, Integrable (f i) Î¼)
    (hf : âˆ€ i, Î¼[f (i + 1) - f i|ğ’¢ i] =áµ[Î¼] 0) : Martingale f ğ’¢ Î¼ := by
  refine martingale_iff.2 âŸ¨supermartingale_of_condexp_sub_nonneg_nat hadp hint fun i â†¦ ?_,
    submartingale_of_condexp_sub_nonneg_nat hadp hint fun i â†¦ (hf i).symm.leâŸ©
  rw [â† neg_sub]
  refine (EventuallyEq.trans ?_ (condexp_neg _).symm).le
  filter_upwards [hf i] with x hx
  simpa only [Pi.zero_apply, Pi.neg_apply, zero_eq_neg]

-- Note that one cannot use `Submartingale.zero_le_of_predictable` to prove the other two
-- corresponding lemmas without imposing more restrictions to the ordering of `E`
/-- A predictable submartingale is a.e. greater equal than its initial state. -/
theorem Submartingale.zero_le_of_predictable [Preorder E] [SigmaFiniteFiltration Î¼ ğ’¢]
    {f : â„• â†’ Î© â†’ E} (hfmgle : Submartingale f ğ’¢ Î¼) (hfadp : Adapted ğ’¢ fun n â†¦ f (n + 1)) (n : â„•) :
    f 0 â‰¤áµ[Î¼] f n := by
  induction' n with k ih
  Â· rfl
  Â· exact ih.trans ((hfmgle.2.1 k (k + 1) k.le_succ).trans_eq <| Germ.coe_eq.mp <|
    congr_arg Germ.ofFun <| condexp_of_stronglyMeasurable (ğ’¢.le _) (hfadp _) <| hfmgle.integrable _)

/-- A predictable supermartingale is a.e. less equal than its initial state. -/
theorem Supermartingale.le_zero_of_predictable [Preorder E] [SigmaFiniteFiltration Î¼ ğ’¢]
    {f : â„• â†’ Î© â†’ E} (hfmgle : Supermartingale f ğ’¢ Î¼) (hfadp : Adapted ğ’¢ fun n â†¦ f (n + 1))
    (n : â„•) : f n â‰¤áµ[Î¼] f 0 := by
  induction' n with k ih
  Â· rfl
  Â· exact ((Germ.coe_eq.mp <| congr_arg Germ.ofFun <| condexp_of_stronglyMeasurable (ğ’¢.le _)
      (hfadp _) <| hfmgle.integrable _).symm.trans_le (hfmgle.2.1 k (k + 1) k.le_succ)).trans ih

/-- A predictable martingale is a.e. equal to its initial state. -/
theorem Martingale.eq_zero_of_predictable [SigmaFiniteFiltration Î¼ ğ’¢] {f : â„• â†’ Î© â†’ E}
    (hfmgle : Martingale f ğ’¢ Î¼) (hfadp : Adapted ğ’¢ fun n â†¦ f (n + 1)) (n : â„•) : f n =áµ[Î¼] f 0 := by
  induction' n with k ih
  Â· rfl
  Â· exact ((Germ.coe_eq.mp (congr_arg Germ.ofFun <| condexp_of_stronglyMeasurable (ğ’¢.le _) (hfadp _)
      (hfmgle.integrable _))).symm.trans (hfmgle.2 k (k + 1) k.le_succ)).trans ih

namespace Submartingale

protected theorem integrable_stoppedValue [LE E] {f : â„• â†’ Î© â†’ E} (hf : Submartingale f ğ’¢ Î¼)
    {Ï„ : Î© â†’ â„•} (hÏ„ : IsStoppingTime ğ’¢ Ï„) {N : â„•} (hbdd : âˆ€ Ï‰, Ï„ Ï‰ â‰¤ N) :
    Integrable (stoppedValue f Ï„) Î¼ :=
  integrable_stoppedValue â„• hÏ„ hf.integrable hbdd

end Submartingale

theorem Submartingale.sum_mul_sub [IsFiniteMeasure Î¼] {R : â„} {Î¾ f : â„• â†’ Î© â†’ â„}
    (hf : Submartingale f ğ’¢ Î¼) (hÎ¾ : Adapted ğ’¢ Î¾) (hbdd : âˆ€ n Ï‰, Î¾ n Ï‰ â‰¤ R)
    (hnonneg : âˆ€ n Ï‰, 0 â‰¤ Î¾ n Ï‰) :
    Submartingale (fun n â†¦ âˆ‘ k âˆˆ Finset.range n, Î¾ k * (f (k + 1) - f k)) ğ’¢ Î¼ := by
  have hÎ¾bdd : âˆ€ i, âˆƒ C, âˆ€ Ï‰, |Î¾ i Ï‰| â‰¤ C := fun i =>
    âŸ¨R, fun Ï‰ => (abs_of_nonneg (hnonneg i Ï‰)).trans_le (hbdd i Ï‰)âŸ©
  have hint : âˆ€ m, Integrable (âˆ‘ k âˆˆ Finset.range m, Î¾ k * (f (k + 1) - f k)) Î¼ := fun m =>
    integrable_finset_sum' _ fun i _ => Integrable.bdd_mul ((hf.integrable _).sub (hf.integrable _))
      hÎ¾.stronglyMeasurable.aestronglyMeasurable (hÎ¾bdd _)
  have hadp : Adapted ğ’¢ fun n â†¦ âˆ‘ k âˆˆ Finset.range n, Î¾ k * (f (k + 1) - f k) := by
    intro m
    refine Finset.stronglyMeasurable_sum' _ fun i hi => ?_
    rw [Finset.mem_range] at hi
    exact (hÎ¾.stronglyMeasurable_le hi.le).mul
      ((hf.adapted.stronglyMeasurable_le (Nat.succ_le_of_lt hi)).sub
        (hf.adapted.stronglyMeasurable_le hi.le))
  refine submartingale_of_condexp_sub_nonneg_nat hadp hint fun i â†¦ ?_
  simp only [â† Finset.sum_Ico_eq_sub _ (Nat.le_succ _), Finset.sum_apply, Pi.mul_apply,
    Pi.sub_apply, Nat.Ico_succ_singleton, Finset.sum_singleton]
  exact EventuallyLE.trans (EventuallyLE.mul_nonneg (Eventually.of_forall (hnonneg _))
    (hf.condexp_sub_nonneg (Nat.le_succ _))) (condexp_stronglyMeasurable_mul (hÎ¾ _)
    (((hf.integrable _).sub (hf.integrable _)).bdd_mul
      hÎ¾.stronglyMeasurable.aestronglyMeasurable (hÎ¾bdd _))
    ((hf.integrable _).sub (hf.integrable _))).symm.le

/-- Given a discrete submartingale `f` and a predictable process `Î¾` (i.e. `Î¾ (n + 1)` is adapted)
the process defined by `fun n â†¦ âˆ‘ k âˆˆ Finset.range n, Î¾ (k + 1) * (f (k + 1) - f k)` is also a
submartingale. -/
theorem Submartingale.sum_mul_sub' [IsFiniteMeasure Î¼] {R : â„} {Î¾ f : â„• â†’ Î© â†’ â„}
    (hf : Submartingale f ğ’¢ Î¼) (hÎ¾ : Adapted ğ’¢ fun n â†¦ Î¾ (n + 1)) (hbdd : âˆ€ n Ï‰, Î¾ n Ï‰ â‰¤ R)
    (hnonneg : âˆ€ n Ï‰, 0 â‰¤ Î¾ n Ï‰) :
    Submartingale (fun n â†¦ âˆ‘ k âˆˆ Finset.range n, Î¾ (k + 1) * (f (k + 1) - f k)) ğ’¢ Î¼ :=
  hf.sum_mul_sub hÎ¾ (fun _ => hbdd _) fun _ => hnonneg _

end Nat

end MeasureTheory
