/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Kexing Ying
-/
module

public import Mathlib.MeasureTheory.Function.ConditionalExpectation.PullOut
public import Mathlib.Probability.Process.Predictable
public import Mathlib.Probability.Process.Stopping

/-!
# Martingales

A family of functions `f : Î¹ â†’ Î© â†’ E` is a martingale with respect to a filtration `â„±` if every
`f i` is integrable, `f` is strongly adapted with respect to `â„±` and for all `i â‰¤ j`,
`Î¼[f j | â„± i] =áµ[Î¼] f i`. On the other hand, `f : Î¹ â†’ Î© â†’ E` is said to be a supermartingale
with respect to the filtration `â„±` if `f i` is integrable, `f` is strongly adapted with respect to
`â„±` and for all `i â‰¤ j`, `Î¼[f j | â„± i] â‰¤áµ[Î¼] f i`. Finally, `f : Î¹ â†’ Î© â†’ E` is said to be a
submartingale with respect to the filtration `â„±` if `f i` is integrable, `f` is strongly adapted
with respect to `â„±` and for all `i â‰¤ j`, `f i â‰¤áµ[Î¼] Î¼[f j | â„± i]`.

### Definitions

* `MeasureTheory.Martingale f â„± Î¼`: `f` is a martingale with respect to filtration `â„±` and
  measure `Î¼`.
* `MeasureTheory.Supermartingale f â„± Î¼`: `f` is a supermartingale with respect to
  filtration `â„±` and measure `Î¼`.
* `MeasureTheory.Submartingale f â„± Î¼`: `f` is a submartingale with respect to filtration `â„±` and
  measure `Î¼`.

### Results

* `MeasureTheory.martingale_condExp f â„± Î¼`: the sequence `fun i => Î¼[f | â„± i]` is a
  martingale with respect to `â„±` and `Î¼`.

-/

@[expose] public section


open TopologicalSpace Filter

open scoped NNReal ENNReal MeasureTheory ProbabilityTheory

namespace MeasureTheory

variable {Î© E Î¹ : Type*} [Preorder Î¹] {m0 : MeasurableSpace Î©} {Î¼ : Measure Î©}
  [NormedAddCommGroup E] [NormedSpace â„ E] [CompleteSpace E] {f g : Î¹ â†’ Î© â†’ E} {â„± : Filtration Î¹ m0}

/-- A family of functions `f : Î¹ â†’ Î© â†’ E` is a martingale with respect to a filtration `â„±` if `f`
is strongly adapted with respect to `â„±` and for all `i â‰¤ j`, `Î¼[f j | â„± i] =áµ[Î¼] f i`. -/
def Martingale (f : Î¹ â†’ Î© â†’ E) (â„± : Filtration Î¹ m0) (Î¼ : Measure Î©) : Prop :=
  StronglyAdapted â„± f âˆ§ âˆ€ i j, i â‰¤ j â†’ Î¼[f j | â„± i] =áµ[Î¼] f i

/-- A family of integrable functions `f : Î¹ â†’ Î© â†’ E` is a supermartingale with respect to a
filtration `â„±` if `f` is strongly adapted with respect to `â„±` and for all `i â‰¤ j`,
`Î¼[f j | â„±.le i] â‰¤áµ[Î¼] f i`. -/
def Supermartingale [LE E] (f : Î¹ â†’ Î© â†’ E) (â„± : Filtration Î¹ m0) (Î¼ : Measure Î©) : Prop :=
  StronglyAdapted â„± f âˆ§ (âˆ€ i j, i â‰¤ j â†’ Î¼[f j | â„± i] â‰¤áµ[Î¼] f i) âˆ§ âˆ€ i, Integrable (f i) Î¼

/-- A family of integrable functions `f : Î¹ â†’ Î© â†’ E` is a submartingale with respect to a
filtration `â„±` if `f` is strongly adapted with respect to `â„±` and for all `i â‰¤ j`,
`f i â‰¤áµ[Î¼] Î¼[f j | â„±.le i]`. -/
def Submartingale [LE E] (f : Î¹ â†’ Î© â†’ E) (â„± : Filtration Î¹ m0) (Î¼ : Measure Î©) : Prop :=
  StronglyAdapted â„± f âˆ§ (âˆ€ i j, i â‰¤ j â†’ f i â‰¤áµ[Î¼] Î¼[f j | â„± i]) âˆ§ âˆ€ i, Integrable (f i) Î¼

theorem martingale_const (â„± : Filtration Î¹ m0) (Î¼ : Measure Î©) [IsFiniteMeasure Î¼] (x : E) :
    Martingale (fun _ _ => x) â„± Î¼ :=
  âŸ¨stronglyAdapted_const â„± _, fun i j _ => by rw [condExp_const (â„±.le _)]âŸ©

theorem martingale_const_fun [OrderBot Î¹] (â„± : Filtration Î¹ m0) (Î¼ : Measure Î©)
    [SigmaFiniteFiltration Î¼ â„±]
    {f : Î© â†’ E} (hf : StronglyMeasurable[â„± âŠ¥] f) (hfint : Integrable f Î¼) :
    Martingale (fun _ => f) â„± Î¼ := by
  refine âŸ¨fun i => hf.mono <| â„±.mono bot_le, fun i j _ => ?_âŸ©
  rw [condExp_of_stronglyMeasurable (â„±.le _) (hf.mono <| â„±.mono bot_le) hfint]

variable (E) in
theorem martingale_zero (â„± : Filtration Î¹ m0) (Î¼ : Measure Î©) : Martingale (0 : Î¹ â†’ Î© â†’ E) â„± Î¼ :=
  âŸ¨stronglyAdapted_zero E â„±, fun i j _ => by simpâŸ©

namespace Martingale

protected theorem stronglyAdapted (hf : Martingale f â„± Î¼) : StronglyAdapted â„± f :=
  hf.1

protected theorem stronglyMeasurable (hf : Martingale f â„± Î¼) (i : Î¹) :
    StronglyMeasurable[â„± i] (f i) :=
  hf.stronglyAdapted i

theorem condExp_ae_eq (hf : Martingale f â„± Î¼) {i j : Î¹} (hij : i â‰¤ j) : Î¼[f j | â„± i] =áµ[Î¼] f i :=
  hf.2 i j hij

protected theorem integrable (hf : Martingale f â„± Î¼) (i : Î¹) : Integrable (f i) Î¼ :=
  integrable_condExp.congr (hf.condExp_ae_eq (le_refl i))

theorem setIntegral_eq [SigmaFiniteFiltration Î¼ â„±] (hf : Martingale f â„± Î¼) {i j : Î¹} (hij : i â‰¤ j)
    {s : Set Î©} (hs : MeasurableSet[â„± i] s) : âˆ« Ï‰ in s, f i Ï‰ âˆ‚Î¼ = âˆ« Ï‰ in s, f j Ï‰ âˆ‚Î¼ := by
  rw [â† setIntegral_condExp (â„±.le i) (hf.integrable j) hs]
  refine setIntegral_congr_ae (â„±.le i s hs) ?_
  filter_upwards [hf.2 i j hij] with _ heq _ using heq.symm

theorem add (hf : Martingale f â„± Î¼) (hg : Martingale g â„± Î¼) : Martingale (f + g) â„± Î¼ := by
  refine âŸ¨hf.stronglyAdapted.add hg.stronglyAdapted, fun i j hij => ?_âŸ©
  exact (condExp_add (hf.integrable j) (hg.integrable j) _).trans
    ((hf.2 i j hij).add (hg.2 i j hij))

theorem neg (hf : Martingale f â„± Î¼) : Martingale (-f) â„± Î¼ :=
  âŸ¨hf.stronglyAdapted.neg, fun i j hij => (condExp_neg ..).trans (hf.2 i j hij).negâŸ©

theorem sub (hf : Martingale f â„± Î¼) (hg : Martingale g â„± Î¼) : Martingale (f - g) â„± Î¼ := by
  rw [sub_eq_add_neg]; exact hf.add hg.neg

theorem smul (c : â„) (hf : Martingale f â„± Î¼) : Martingale (c â€¢ f) â„± Î¼ := by
  refine âŸ¨hf.stronglyAdapted.smul c, fun i j hij => ?_âŸ©
  refine (condExp_smul ..).trans ((hf.2 i j hij).mono fun x hx => ?_)
  simp only [Pi.smul_apply, hx]

theorem supermartingale [Preorder E] (hf : Martingale f â„± Î¼) : Supermartingale f â„± Î¼ :=
  âŸ¨hf.1, fun i j hij => (hf.2 i j hij).le, fun i => hf.integrable iâŸ©

theorem submartingale [Preorder E] (hf : Martingale f â„± Î¼) : Submartingale f â„± Î¼ :=
  âŸ¨hf.1, fun i j hij => (hf.2 i j hij).symm.le, fun i => hf.integrable iâŸ©

end Martingale

theorem martingale_iff [PartialOrder E] :
    Martingale f â„± Î¼ â†” Supermartingale f â„± Î¼ âˆ§ Submartingale f â„± Î¼ :=
  âŸ¨fun hf => âŸ¨hf.supermartingale, hf.submartingaleâŸ©, fun âŸ¨hfâ‚, hfâ‚‚âŸ© =>
    âŸ¨hfâ‚.1, fun i j hij => (hfâ‚.2.1 i j hij).antisymm (hfâ‚‚.2.1 i j hij)âŸ©âŸ©

theorem martingale_condExp (f : Î© â†’ E) (â„± : Filtration Î¹ m0) (Î¼ : Measure Î©)
    [SigmaFiniteFiltration Î¼ â„±] : Martingale (fun i => Î¼[f | â„± i]) â„± Î¼ :=
  âŸ¨fun _ => stronglyMeasurable_condExp, fun _ j hij => condExp_condExp_of_le (â„±.mono hij) (â„±.le j)âŸ©

namespace Supermartingale

protected theorem stronglyAdapted [LE E] (hf : Supermartingale f â„± Î¼) : StronglyAdapted â„± f :=
  hf.1

protected theorem stronglyMeasurable [LE E] (hf : Supermartingale f â„± Î¼) (i : Î¹) :
    StronglyMeasurable[â„± i] (f i) :=
  hf.stronglyAdapted i

protected theorem integrable [LE E] (hf : Supermartingale f â„± Î¼) (i : Î¹) : Integrable (f i) Î¼ :=
  hf.2.2 i

theorem condExp_ae_le [LE E] (hf : Supermartingale f â„± Î¼) {i j : Î¹} (hij : i â‰¤ j) :
    Î¼[f j | â„± i] â‰¤áµ[Î¼] f i :=
  hf.2.1 i j hij

theorem setIntegral_le [PartialOrder E] [IsOrderedAddMonoid E] [IsOrderedModule â„ E]
    [ClosedIciTopology E] [SigmaFiniteFiltration Î¼ â„±] {f : Î¹ â†’ Î© â†’ E} (hf : Supermartingale f â„± Î¼)
    {i j : Î¹} (hij : i â‰¤ j) {s : Set Î©} (hs : MeasurableSet[â„± i] s) :
    âˆ« Ï‰ in s, f j Ï‰ âˆ‚Î¼ â‰¤ âˆ« Ï‰ in s, f i Ï‰ âˆ‚Î¼ := by
  rw [â† setIntegral_condExp (â„±.le i) (hf.integrable j) hs]
  refine setIntegral_mono_ae integrable_condExp.integrableOn (hf.integrable i).integrableOn ?_
  filter_upwards [hf.2.1 i j hij] with _ heq using heq

theorem add [Preorder E] [AddLeftMono E] (hf : Supermartingale f â„± Î¼)
    (hg : Supermartingale g â„± Î¼) : Supermartingale (f + g) â„± Î¼ := by
  refine âŸ¨hf.1.add hg.1, fun i j hij => ?_, fun i => (hf.2.2 i).add (hg.2.2 i)âŸ©
  refine (condExp_add (hf.integrable j) (hg.integrable j) _).le.trans ?_
  filter_upwards [hf.2.1 i j hij, hg.2.1 i j hij]
  intros
  refine add_le_add ?_ ?_ <;> assumption

theorem add_martingale [Preorder E] [AddLeftMono E]
    (hf : Supermartingale f â„± Î¼) (hg : Martingale g â„± Î¼) : Supermartingale (f + g) â„± Î¼ :=
  hf.add hg.supermartingale

theorem neg [Preorder E] [AddLeftMono E] (hf : Supermartingale f â„± Î¼) :
    Submartingale (-f) â„± Î¼ := by
  refine âŸ¨hf.1.neg, fun i j hij => ?_, fun i => (hf.2.2 i).negâŸ©
  refine EventuallyLE.trans ?_ (condExp_neg ..).symm.le
  filter_upwards [hf.2.1 i j hij] with _ _
  simpa

end Supermartingale

namespace Submartingale

protected theorem stronglyAdapted [LE E] (hf : Submartingale f â„± Î¼) : StronglyAdapted â„± f :=
  hf.1

protected theorem stronglyMeasurable [LE E] (hf : Submartingale f â„± Î¼) (i : Î¹) :
    StronglyMeasurable[â„± i] (f i) :=
  hf.stronglyAdapted i

protected theorem integrable [LE E] (hf : Submartingale f â„± Î¼) (i : Î¹) : Integrable (f i) Î¼ :=
  hf.2.2 i

theorem ae_le_condExp [LE E] (hf : Submartingale f â„± Î¼) {i j : Î¹} (hij : i â‰¤ j) :
    f i â‰¤áµ[Î¼] Î¼[f j | â„± i] :=
  hf.2.1 i j hij

theorem add [Preorder E] [AddLeftMono E] (hf : Submartingale f â„± Î¼)
    (hg : Submartingale g â„± Î¼) : Submartingale (f + g) â„± Î¼ := by
  refine âŸ¨hf.1.add hg.1, fun i j hij => ?_, fun i => (hf.2.2 i).add (hg.2.2 i)âŸ©
  refine EventuallyLE.trans ?_ (condExp_add (hf.integrable j) (hg.integrable j) _).symm.le
  filter_upwards [hf.2.1 i j hij, hg.2.1 i j hij]
  intros
  refine add_le_add ?_ ?_ <;> assumption

theorem add_martingale [Preorder E] [AddLeftMono E] (hf : Submartingale f â„± Î¼)
    (hg : Martingale g â„± Î¼) : Submartingale (f + g) â„± Î¼ :=
  hf.add hg.submartingale

theorem neg [Preorder E] [AddLeftMono E] (hf : Submartingale f â„± Î¼) :
    Supermartingale (-f) â„± Î¼ := by
  refine âŸ¨hf.1.neg, fun i j hij => (condExp_neg ..).le.trans ?_, fun i => (hf.2.2 i).negâŸ©
  filter_upwards [hf.2.1 i j hij] with _ _
  simpa

/-- The converse of this lemma is `MeasureTheory.submartingale_of_setIntegral_le`. -/
theorem setIntegral_le [PartialOrder E] [IsOrderedAddMonoid E] [IsOrderedModule â„ E]
    [ClosedIciTopology E] [SigmaFiniteFiltration Î¼ â„±] {f : Î¹ â†’ Î© â†’ E} (hf : Submartingale f â„± Î¼)
    {i j : Î¹} (hij : i â‰¤ j) {s : Set Î©} (hs : MeasurableSet[â„± i] s) :
    âˆ« Ï‰ in s, f i Ï‰ âˆ‚Î¼ â‰¤ âˆ« Ï‰ in s, f j Ï‰ âˆ‚Î¼ := by
  rw [â† neg_le_neg_iff, â† integral_neg, â† integral_neg]
  exact Supermartingale.setIntegral_le hf.neg hij hs

theorem sub_supermartingale [Preorder E] [AddLeftMono E]
    (hf : Submartingale f â„± Î¼) (hg : Supermartingale g â„± Î¼) : Submartingale (f - g) â„± Î¼ := by
  rw [sub_eq_add_neg]; exact hf.add hg.neg

theorem sub_martingale [Preorder E] [AddLeftMono E] (hf : Submartingale f â„± Î¼)
    (hg : Martingale g â„± Î¼) : Submartingale (f - g) â„± Î¼ :=
  hf.sub_supermartingale hg.supermartingale

protected theorem sup [Lattice E] [ContinuousSup E] [HasSolidNorm E] [IsOrderedAddMonoid E]
    [IsOrderedModule â„ E] {f g : Î¹ â†’ Î© â†’ E} (hf : Submartingale f â„± Î¼)
    (hg : Submartingale g â„± Î¼) :
    Submartingale (f âŠ” g) â„± Î¼ := by
  refine âŸ¨fun i =>
    @StronglyMeasurable.sup _ _ _ _ (â„± i) _ _ _ (hf.stronglyAdapted i) (hg.stronglyAdapted i),
    fun i j hij => ?_, fun i => Integrable.sup (hf.integrable _) (hg.integrable _)âŸ©
  refine EventuallyLE.sup_le ?_ ?_
  Â· exact EventuallyLE.trans (hf.2.1 i j hij)
      (condExp_mono (hf.integrable _) (Integrable.sup (hf.integrable j) (hg.integrable j))
        (Eventually.of_forall fun x => le_sup_left))
  Â· exact EventuallyLE.trans (hg.2.1 i j hij)
      (condExp_mono (hg.integrable _) (Integrable.sup (hf.integrable j) (hg.integrable j))
        (Eventually.of_forall fun x => le_sup_right))

protected theorem pos [Lattice E] [ContinuousSup E] [HasSolidNorm E] [IsOrderedAddMonoid E]
    [IsOrderedModule â„ E] {f : Î¹ â†’ Î© â†’ E} (hf : Submartingale f â„± Î¼) :
    Submartingale (fâº) â„± Î¼ :=
  hf.sup (martingale_zero _ _ _).submartingale

end Submartingale

section Submartingale

theorem submartingale_of_setIntegral_le [SigmaFiniteFiltration Î¼ â„±]
    {f : Î¹ â†’ Î© â†’ â„} (hadp : StronglyAdapted â„± f)
    (hint : âˆ€ i, Integrable (f i) Î¼) (hf : âˆ€ i j : Î¹,
      i â‰¤ j â†’ âˆ€ s : Set Î©, MeasurableSet[â„± i] s â†’ âˆ« Ï‰ in s, f i Ï‰ âˆ‚Î¼ â‰¤ âˆ« Ï‰ in s, f j Ï‰ âˆ‚Î¼) :
    Submartingale f â„± Î¼ := by
  refine âŸ¨hadp, fun i j hij => ?_, hintâŸ©
  suffices f i â‰¤áµ[Î¼.trim (â„±.le i)] Î¼[f j | â„± i] by exact ae_le_of_ae_le_trim this
  suffices 0 â‰¤áµ[Î¼.trim (â„±.le i)] Î¼[f j | â„± i] - f i by
    filter_upwards [this] with x hx
    rwa [â† sub_nonneg]
  refine ae_nonneg_of_forall_setIntegral_nonneg
    ((integrable_condExp.sub (hint i)).trim _ (stronglyMeasurable_condExp.sub <| hadp i))
      fun s hs _ => ?_
  specialize hf i j hij s hs
  rwa [â† setIntegral_trim _ (stronglyMeasurable_condExp.sub <| hadp i) hs,
    integral_sub' integrable_condExp.integrableOn (hint i).integrableOn, sub_nonneg,
    setIntegral_condExp (â„±.le i) (hint j) hs]

theorem submartingale_of_condExp_sub_nonneg [PartialOrder E] [IsOrderedAddMonoid E]
    [SigmaFiniteFiltration Î¼ â„±] {f : Î¹ â†’ Î© â†’ E} (hadp : StronglyAdapted â„± f)
    (hint : âˆ€ i, Integrable (f i) Î¼) (hf : âˆ€ i j, i â‰¤ j â†’ 0 â‰¤áµ[Î¼] Î¼[f j - f i | â„± i]) :
    Submartingale f â„± Î¼ := by
  refine âŸ¨hadp, fun i j hij => ?_, hintâŸ©
  rw [â† condExp_of_stronglyMeasurable (â„±.le _) (hadp _) (hint _), â† eventually_sub_nonneg]
  exact EventuallyLE.trans (hf i j hij) (condExp_sub (hint _) (hint _) _).le

theorem Submartingale.condExp_sub_nonneg [PartialOrder E] [IsOrderedAddMonoid E]
    {f : Î¹ â†’ Î© â†’ E} (hf : Submartingale f â„± Î¼) {i j : Î¹}
    (hij : i â‰¤ j) : 0 â‰¤áµ[Î¼] Î¼[f j - f i | â„± i] := by
  by_cases h : SigmaFinite (Î¼.trim (â„±.le i))
  swap; Â· rw [condExp_of_not_sigmaFinite (â„±.le i) h]
  refine EventuallyLE.trans ?_ (condExp_sub (hf.integrable _) (hf.integrable _) _).symm.le
  rw [eventually_sub_nonneg,
    condExp_of_stronglyMeasurable (â„±.le _) (hf.stronglyAdapted _) (hf.integrable _)]
  exact hf.2.1 i j hij

theorem submartingale_iff_condExp_sub_nonneg [PartialOrder E] [IsOrderedAddMonoid E]
    [SigmaFiniteFiltration Î¼ â„±] {f : Î¹ â†’ Î© â†’ E} :
    Submartingale f â„± Î¼ â†”
      StronglyAdapted â„± f âˆ§ (âˆ€ i, Integrable (f i) Î¼) âˆ§ âˆ€ i j, i â‰¤ j â†’ 0 â‰¤áµ[Î¼] Î¼[f j - f i | â„± i] :=
  âŸ¨fun h => âŸ¨h.stronglyAdapted, h.integrable, fun _ _ => h.condExp_sub_nonnegâŸ©,
   fun âŸ¨hadp, hint, hâŸ© => submartingale_of_condExp_sub_nonneg hadp hint hâŸ©

end Submartingale

namespace Supermartingale

theorem sub_submartingale [Preorder E] [AddLeftMono E]
    (hf : Supermartingale f â„± Î¼) (hg : Submartingale g â„± Î¼) : Supermartingale (f - g) â„± Î¼ := by
  rw [sub_eq_add_neg]; exact hf.add hg.neg

theorem sub_martingale [Preorder E] [AddLeftMono E]
    (hf : Supermartingale f â„± Î¼) (hg : Martingale g â„± Î¼) : Supermartingale (f - g) â„± Î¼ :=
  hf.sub_submartingale hg.submartingale

section

variable {F : Type*} [NormedAddCommGroup F] [PartialOrder F] [NormedSpace â„ F] [CompleteSpace F]
  [IsOrderedModule â„ F]

theorem smul_nonneg {f : Î¹ â†’ Î© â†’ F} {c : â„} (hc : 0 â‰¤ c) (hf : Supermartingale f â„± Î¼) :
    Supermartingale (c â€¢ f) â„± Î¼ := by
  refine âŸ¨hf.1.smul c, fun i j hij => ?_, fun i => (hf.2.2 i).smul câŸ©
  filter_upwards [condExp_smul c (f j) (â„± i), hf.2.1 i j hij] with Ï‰ hÏ‰ hle
  simpa only [hÏ‰, Pi.smul_apply] using smul_le_smul_of_nonneg_left hle hc

theorem smul_nonpos [IsOrderedAddMonoid F] {f : Î¹ â†’ Î© â†’ F} {c : â„}
    (hc : c â‰¤ 0) (hf : Supermartingale f â„± Î¼) :
    Submartingale (c â€¢ f) â„± Î¼ := by
  rw [â† neg_neg c, neg_smul]
  exact (hf.smul_nonneg <| neg_nonneg.2 hc).neg

end

end Supermartingale

namespace Submartingale

section

variable {F : Type*} [NormedAddCommGroup F] [PartialOrder F] [IsOrderedAddMonoid F]
  [NormedSpace â„ F] [CompleteSpace F] [IsOrderedModule â„ F]

theorem smul_nonneg {f : Î¹ â†’ Î© â†’ F} {c : â„} (hc : 0 â‰¤ c) (hf : Submartingale f â„± Î¼) :
    Submartingale (c â€¢ f) â„± Î¼ := by
  rw [â† neg_neg (c â€¢ f), â† smul_neg]
  exact Supermartingale.neg (hf.neg.smul_nonneg hc)

theorem smul_nonpos {f : Î¹ â†’ Î© â†’ F} {c : â„} (hc : c â‰¤ 0) (hf : Submartingale f â„± Î¼) :
    Supermartingale (c â€¢ f) â„± Î¼ := by
  rw [â† neg_neg c, neg_smul]
  exact (hf.smul_nonneg <| neg_nonneg.2 hc).neg

end

end Submartingale

section Nat

variable {ğ’¢ : Filtration â„• m0}

section SubSuper

section OfSetIntegral

theorem submartingale_of_setIntegral_le_succ [IsFiniteMeasure Î¼] {f : â„• â†’ Î© â†’ â„}
    (hadp : StronglyAdapted ğ’¢ f) (hint : âˆ€ i, Integrable (f i) Î¼)
    (hf : âˆ€ i, âˆ€ s : Set Î©, MeasurableSet[ğ’¢ i] s â†’ âˆ« Ï‰ in s, f i Ï‰ âˆ‚Î¼ â‰¤ âˆ« Ï‰ in s, f (i + 1) Ï‰ âˆ‚Î¼) :
    Submartingale f ğ’¢ Î¼ := by
  refine submartingale_of_setIntegral_le hadp hint fun i j hij s hs => ?_
  induction hij with
  | refl => rfl
  | step hkâ‚ hkâ‚‚ => exact hkâ‚‚.trans (hf _ s (ğ’¢.mono hkâ‚ _ hs))

theorem supermartingale_of_setIntegral_succ_le [IsFiniteMeasure Î¼] {f : â„• â†’ Î© â†’ â„}
    (hadp : StronglyAdapted ğ’¢ f) (hint : âˆ€ i, Integrable (f i) Î¼)
    (hf : âˆ€ i, âˆ€ s : Set Î©, MeasurableSet[ğ’¢ i] s â†’ âˆ« Ï‰ in s, f (i + 1) Ï‰ âˆ‚Î¼ â‰¤ âˆ« Ï‰ in s, f i Ï‰ âˆ‚Î¼) :
    Supermartingale f ğ’¢ Î¼ := by
  rw [â† neg_neg f]
  refine (submartingale_of_setIntegral_le_succ hadp.neg (fun i => (hint i).neg) ?_).neg
  simpa only [integral_neg, Pi.neg_apply, neg_le_neg_iff]

end OfSetIntegral

section OfSucc

variable [PartialOrder E] [IsOrderedAddMonoid E] [ClosedIciTopology E] [IsOrderedModule â„ E]

theorem submartingale_nat [IsFiniteMeasure Î¼] {f : â„• â†’ Î© â†’ E} (hadp : StronglyAdapted ğ’¢ f)
    (hint : âˆ€ i, Integrable (f i) Î¼) (hf : âˆ€ i, f i â‰¤áµ[Î¼] Î¼[f (i + 1) | ğ’¢ i]) :
    Submartingale f ğ’¢ Î¼ := by
  refine âŸ¨hadp, fun i j hij â†¦ ?_, hintâŸ©
  induction j, hij using Nat.le_induction with
  | base =>
    refine ae_of_all _ fun _ â†¦ ?_
    rw [condExp_of_stronglyMeasurable (ğ’¢.le i) (hadp i) (hint i)]
  | succ k hik hk =>
    filter_upwards [hk, condExp_mono (hint k) integrable_condExp (hf k),
      ğ’¢.condExp_condExp (f (k + 1)) hik] with Ï‰ hÏ‰1 hÏ‰2 hÏ‰3
    grw [hÏ‰1, hÏ‰2, hÏ‰3]

theorem supermartingale_nat [IsFiniteMeasure Î¼] {f : â„• â†’ Î© â†’ E} (hadp : StronglyAdapted ğ’¢ f)
    (hint : âˆ€ i, Integrable (f i) Î¼) (hf : âˆ€ i, Î¼[f (i + 1) | ğ’¢ i] â‰¤áµ[Î¼] f i) :
    Supermartingale f ğ’¢ Î¼ := by
  rw [â† neg_neg f]
  refine (submartingale_nat hadp.neg (fun i => (hint i).neg) fun i =>
    EventuallyLE.trans ?_ (condExp_neg ..).symm.le).neg
  filter_upwards [hf i] with x hx using neg_le_neg hx

theorem submartingale_of_condExp_sub_nonneg_nat [IsFiniteMeasure Î¼] {f : â„• â†’ Î© â†’ E}
    (hadp : StronglyAdapted ğ’¢ f) (hint : âˆ€ i, Integrable (f i) Î¼)
    (hf : âˆ€ i, 0 â‰¤áµ[Î¼] Î¼[f (i + 1) - f i | ğ’¢ i]) : Submartingale f ğ’¢ Î¼ := by
  refine submartingale_nat hadp hint fun i => ?_
  rw [â† condExp_of_stronglyMeasurable (ğ’¢.le _) (hadp _) (hint _), â† eventually_sub_nonneg]
  exact EventuallyLE.trans (hf i) (condExp_sub (hint _) (hint _) _).le

theorem supermartingale_of_condExp_sub_nonneg_nat [IsFiniteMeasure Î¼] {f : â„• â†’ Î© â†’ E}
    (hadp : StronglyAdapted ğ’¢ f) (hint : âˆ€ i, Integrable (f i) Î¼)
    (hf : âˆ€ i, 0 â‰¤áµ[Î¼] Î¼[f i - f (i + 1) | ğ’¢ i]) : Supermartingale f ğ’¢ Î¼ := by
  rw [â† neg_neg f]
  refine (submartingale_of_condExp_sub_nonneg_nat hadp.neg (fun i => (hint i).neg) ?_).neg
  simpa only [Pi.zero_apply, Pi.neg_apply, neg_sub_neg]

end OfSucc

section Preorder

variable [Preorder E]

-- Note that one cannot use `Submartingale.zero_le_of_predictable` to prove the other two
-- corresponding lemmas without imposing more restrictions to the ordering of `E`
/-- A predictable submartingale is a.e. greater than or equal to its initial state. -/
theorem Submartingale.zero_le_of_predictable [SigmaFiniteFiltration Î¼ ğ’¢] {f : â„• â†’ Î© â†’ E}
    (hfmgle : Submartingale f ğ’¢ Î¼) (hfadp : StronglyAdapted ğ’¢ fun n => f (n + 1)) (n : â„•) :
    f 0 â‰¤áµ[Î¼] f n := by
  induction n with
  | zero => rfl
  | succ k ih =>
    exact ih.trans ((hfmgle.2.1 k (k + 1) k.le_succ).trans_eq <| Germ.coe_eq.mp <|
    congr_arg Germ.ofFun <| condExp_of_stronglyMeasurable (ğ’¢.le _) (hfadp _) <| hfmgle.integrable _)

/-- A predictable supermartingale is a.e. less than or equal to its initial state. -/
theorem Supermartingale.le_zero_of_predictable [SigmaFiniteFiltration Î¼ ğ’¢] {f : â„• â†’ Î© â†’ E}
    (hfmgle : Supermartingale f ğ’¢ Î¼) (hfadp : StronglyAdapted ğ’¢ fun n => f (n + 1))
    (n : â„•) : f n â‰¤áµ[Î¼] f 0 := by
  induction n with
  | zero => rfl
  | succ k ih =>
    exact ((Germ.coe_eq.mp <| congr_arg Germ.ofFun <| condExp_of_stronglyMeasurable (ğ’¢.le _)
      (hfadp _) <| hfmgle.integrable _).symm.trans_le (hfmgle.2.1 k (k + 1) k.le_succ)).trans ih

end Preorder

end SubSuper

theorem martingale_nat [IsFiniteMeasure Î¼] {f : â„• â†’ Î© â†’ E} (hadp : StronglyAdapted ğ’¢ f)
    (hint : âˆ€ i, Integrable (f i) Î¼) (hf : âˆ€ i, f i =áµ[Î¼] Î¼[f (i + 1) | ğ’¢ i]) :
    Martingale f ğ’¢ Î¼ := by
  refine âŸ¨hadp, fun i j hij â†¦ ?_âŸ©
  induction j, hij using Nat.le_induction with
  | base =>
    refine ae_of_all _ fun _ â†¦ ?_
    rw [condExp_of_stronglyMeasurable (ğ’¢.le i) (hadp i) (hint i)]
  | succ k hik hk =>
    filter_upwards [hk, condExp_congr_ae (hf k), ğ’¢.condExp_condExp (f (k + 1)) hik]
      with Ï‰ hÏ‰1 hÏ‰2 hÏ‰3
    rw [â† hÏ‰1, hÏ‰2, hÏ‰3]

theorem martingale_of_setIntegral_eq_succ [IsFiniteMeasure Î¼] {f : â„• â†’ Î© â†’ E}
    (hadp : StronglyAdapted ğ’¢ f) (hint : âˆ€ i, Integrable (f i) Î¼)
    (hf : âˆ€ i, âˆ€ s : Set Î©, MeasurableSet[ğ’¢ i] s â†’ âˆ« Ï‰ in s, f i Ï‰ âˆ‚Î¼ = âˆ« Ï‰ in s, f (i + 1) Ï‰ âˆ‚Î¼) :
    Martingale f ğ’¢ Î¼ := by
  refine martingale_nat hadp hint fun n â†¦ ae_eq_of_ae_eq_trim <|
    ((hint n).trim (ğ’¢.le n) (hadp n)).ae_eq_of_forall_setIntegral_eq _ _
    (integrable_condExp.trim (ğ’¢.le n) stronglyMeasurable_condExp) fun s ms hs â†¦ ?_
  rw [â† setIntegral_trim (ğ’¢.le n) (hadp n) ms,
    â† setIntegral_trim (ğ’¢.le n) stronglyMeasurable_condExp ms,
    setIntegral_condExp (ğ’¢.le n) (hint (n + 1)) ms, hf n s ms]

theorem martingale_of_condExp_sub_eq_zero_nat [IsFiniteMeasure Î¼] {f : â„• â†’ Î© â†’ E}
    (hadp : StronglyAdapted ğ’¢ f) (hint : âˆ€ i, Integrable (f i) Î¼)
    (hf : âˆ€ i, Î¼[f (i + 1) - f i | ğ’¢ i] =áµ[Î¼] 0) : Martingale f ğ’¢ Î¼ := by
  refine martingale_nat hadp hint fun i â†¦ ?_
  rw [â† condExp_of_stronglyMeasurable (ğ’¢.le _) (hadp _) (hint _),
    eventuallyEq_comm, eventuallyEq_iff_sub]
  exact EventuallyEq.trans (condExp_sub (hint _) (hint _) _).symm (hf i)

/-- A predictable martingale is a.e. equal to its initial state. -/
theorem Martingale.eq_zero_of_predictable [SigmaFiniteFiltration Î¼ ğ’¢] {f : â„• â†’ Î© â†’ E}
    (hfmgle : Martingale f ğ’¢ Î¼) (hfadp : StronglyAdapted ğ’¢ fun n => f (n + 1)) (n : â„•) :
    f n =áµ[Î¼] f 0 := by
  induction n with
  | zero => rfl
  | succ k ih =>
    exact ((Germ.coe_eq.mp (congr_arg Germ.ofFun <| condExp_of_stronglyMeasurable (ğ’¢.le _) (hfadp _)
      (hfmgle.integrable _))).symm.trans (hfmgle.2 k (k + 1) k.le_succ)).trans ih

section IsPredictable

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
    [CompleteSpace E] [MeasurableSpace E] [BorelSpace E] [SecondCountableTopology E]

/-- A predictable submartingale is a.e. greater than or equal to its initial state.

In contrast to the non-primed version, this result requires second countability as
`StronglyAdapted` is defined using strong measurability while `IsPredictable` only provides
measurable. -/
theorem Submartingale.zero_le_of_predictable' [Preorder E] [SigmaFiniteFiltration Î¼ ğ’¢]
    {f : â„• â†’ Î© â†’ E} (hfmgle : Submartingale f ğ’¢ Î¼) (hf : IsPredictable ğ’¢ f) (n : â„•) :
    f 0 â‰¤áµ[Î¼] f n :=
  zero_le_of_predictable hfmgle (fun _ â†¦ (hf.measurable_add_one _).stronglyMeasurable) n

/-- A predictable supermartingale is a.e. less than or equal to its initial state.

In contrast to the non-primed version, this result requires second countability as `StronglyAdapted`
is defined using strong measurability while `IsPredictable` only provides measurable. -/
theorem Supermartingale.le_zero_of_predictable' [Preorder E] [SigmaFiniteFiltration Î¼ ğ’¢]
    {f : â„• â†’ Î© â†’ E} (hfmgle : Supermartingale f ğ’¢ Î¼) (hfadp : IsPredictable ğ’¢ f)
    (n : â„•) : f n â‰¤áµ[Î¼] f 0 :=
  le_zero_of_predictable hfmgle (fun _ â†¦ (hfadp.measurable_add_one _).stronglyMeasurable) n

/-- A predictable martingale is a.e. equal to its initial state.

In contrast to the non-primed version, this result requires second countability as `StronglyAdapted`
is defined using strong measurability while `IsPredictable` only provides measurable. -/
theorem Martingale.eq_zero_of_predictable' [SigmaFiniteFiltration Î¼ ğ’¢] {f : â„• â†’ Î© â†’ E}
    (hfmgle : Martingale f ğ’¢ Î¼) (hfadp : IsPredictable ğ’¢ f) (n : â„•) : f n =áµ[Î¼] f 0 :=
  eq_zero_of_predictable hfmgle (fun _ â†¦ (hfadp.measurable_add_one _).stronglyMeasurable) n

end IsPredictable

namespace Submartingale

protected theorem integrable_stoppedValue [LE E] {f : â„• â†’ Î© â†’ E} (hf : Submartingale f ğ’¢ Î¼)
    {Ï„ : Î© â†’ â„•âˆ} (hÏ„ : IsStoppingTime ğ’¢ Ï„) {N : â„•} (hbdd : âˆ€ Ï‰, Ï„ Ï‰ â‰¤ N) :
    Integrable (stoppedValue f Ï„) Î¼ :=
  integrable_stoppedValue â„• hÏ„ hf.integrable hbdd

end Submartingale

section SumSMul

variable [PartialOrder E] [IsOrderedModule â„ E] [ClosedIciTopology E] [IsOrderedAddMonoid E]

theorem Submartingale.sum_smul_sub [IsFiniteMeasure Î¼] {R : â„} {f : â„• â†’ Î© â†’ E} {Î¾ : â„• â†’ Î© â†’ â„}
    (hf : Submartingale f ğ’¢ Î¼) (hÎ¾ : StronglyAdapted ğ’¢ Î¾) (hbdd : âˆ€ n Ï‰, Î¾ n Ï‰ â‰¤ R)
    (hnonneg : âˆ€ n Ï‰, 0 â‰¤ Î¾ n Ï‰) :
    Submartingale (fun n => âˆ‘ k âˆˆ Finset.range n, Î¾ k â€¢ (f (k + 1) - f k)) ğ’¢ Î¼ := by
  have hÎ¾bdd : âˆ€ i, âˆƒ C, âˆ€ Ï‰, â€–Î¾ i Ï‰â€– â‰¤ C := fun i =>
    âŸ¨R, fun Ï‰ => (abs_of_nonneg (hnonneg i Ï‰)).trans_le (hbdd i Ï‰)âŸ©
  choose C hC using hÎ¾bdd
  have hint : âˆ€ m, Integrable (âˆ‘ k âˆˆ Finset.range m, Î¾ k â€¢ (f (k + 1) - f k)) Î¼ := fun m =>
      integrable_finset_sum' _ fun i _ => Integrable.bdd_smul
        ((hf.integrable _).sub (hf.integrable _)) (C i)
        hÎ¾.stronglyMeasurable.aestronglyMeasurable (ae_of_all _ (hC i))
  have hadp : StronglyAdapted ğ’¢ fun n => âˆ‘ k âˆˆ Finset.range n, Î¾ k â€¢ (f (k + 1) - f k) := by
    intro m
    refine Finset.stronglyMeasurable_sum _ fun i hi => ?_
    rw [Finset.mem_range] at hi
    exact (hÎ¾.stronglyMeasurable_le hi.le).smul
      ((hf.stronglyAdapted.stronglyMeasurable_le (Nat.succ_le_of_lt hi)).sub
        (hf.stronglyAdapted.stronglyMeasurable_le hi.le))
  refine submartingale_of_condExp_sub_nonneg_nat hadp hint fun i => ?_
  simp only [â† Finset.sum_Ico_eq_sub _ (Nat.le_succ _), Nat.succ_eq_add_one, Nat.Ico_succ_singleton,
    Finset.sum_singleton]
  filter_upwards [hf.condExp_sub_nonneg i.le_succ,
    condExp_smul_of_aestronglyMeasurable_left (hÎ¾ i).aestronglyMeasurable
      (((hf.integrable (i + 1)).sub (hf.integrable i)).bdd_smul
      (C i) hÎ¾.stronglyMeasurable.aestronglyMeasurable (ae_of_all _ (hC i)))
      ((hf.integrable _).sub (hf.integrable _))] with Ï‰ hÏ‰1 hÏ‰2
  simp only [Pi.zero_apply, Nat.succ_eq_add_one, Pi.smul_apply'] at hÏ‰1 hÏ‰2 âŠ¢
  grw [â† smul_zero (0 : â„), hnonneg i Ï‰, hÏ‰1, hÏ‰2]
  Â· exact hnonneg i Ï‰
  Â· simp

/-- Given a discrete submartingale `f` and a predictable process `Î¾` (i.e. `Î¾ (n + 1)` is strongly
adapted) the process defined by `fun n => âˆ‘ k âˆˆ Finset.range n, Î¾ (k + 1) * (f (k + 1) - f k)` is
also a submartingale. -/
theorem Submartingale.sum_smul_sub' [IsFiniteMeasure Î¼] {R : â„} {Î¾ : â„• â†’ Î© â†’ â„} {f : â„• â†’ Î© â†’ E}
    (hf : Submartingale f ğ’¢ Î¼) (hÎ¾ : StronglyAdapted ğ’¢ fun n => Î¾ (n + 1)) (hbdd : âˆ€ n Ï‰, Î¾ n Ï‰ â‰¤ R)
    (hnonneg : âˆ€ n Ï‰, 0 â‰¤ Î¾ n Ï‰) :
    Submartingale (fun n => âˆ‘ k âˆˆ Finset.range n, Î¾ (k + 1) â€¢ (f (k + 1) - f k)) ğ’¢ Î¼ :=
  hf.sum_smul_sub hÎ¾ (fun _ => hbdd _) fun _ => hnonneg _

theorem Submartingale.sum_mul_sub [IsFiniteMeasure Î¼] {R : â„} {Î¾ f : â„• â†’ Î© â†’ â„}
    (hf : Submartingale f ğ’¢ Î¼) (hÎ¾ : StronglyAdapted ğ’¢ Î¾) (hbdd : âˆ€ n Ï‰, Î¾ n Ï‰ â‰¤ R)
    (hnonneg : âˆ€ n Ï‰, 0 â‰¤ Î¾ n Ï‰) :
    Submartingale (fun n => âˆ‘ k âˆˆ Finset.range n, Î¾ k * (f (k + 1) - f k)) ğ’¢ Î¼ :=
  hf.sum_smul_sub hÎ¾ hbdd hnonneg

/-- Given a discrete submartingale `f` and a predictable process `Î¾` (i.e. `Î¾ (n + 1)` is strongly
adapted) the process defined by `fun n => âˆ‘ k âˆˆ Finset.range n, Î¾ (k + 1) * (f (k + 1) - f k)` is
also a submartingale. -/
theorem Submartingale.sum_mul_sub' [IsFiniteMeasure Î¼] {R : â„} {Î¾ f : â„• â†’ Î© â†’ â„}
    (hf : Submartingale f ğ’¢ Î¼) (hÎ¾ : StronglyAdapted ğ’¢ fun n => Î¾ (n + 1)) (hbdd : âˆ€ n Ï‰, Î¾ n Ï‰ â‰¤ R)
    (hnonneg : âˆ€ n Ï‰, 0 â‰¤ Î¾ n Ï‰) :
    Submartingale (fun n => âˆ‘ k âˆˆ Finset.range n, Î¾ (k + 1) * (f (k + 1) - f k)) ğ’¢ Î¼ :=
  hf.sum_smul_sub' hÎ¾ hbdd hnonneg

end SumSMul

end Nat

end MeasureTheory
