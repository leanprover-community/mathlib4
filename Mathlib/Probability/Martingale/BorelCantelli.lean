/-
Copyright (c) 2022 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
module

public import Mathlib.Algebra.Order.Archimedean.IndicatorCard
public import Mathlib.Probability.Martingale.Centering
public import Mathlib.Probability.Martingale.Convergence
public import Mathlib.Probability.Martingale.OptionalStopping

/-!

# Generalized Borel-Cantelli lemma

This file proves L√©vy's generalized Borel-Cantelli lemma which is a generalization of the
Borel-Cantelli lemmas. With this generalization, one can easily deduce the Borel-Cantelli lemmas
by choosing appropriate filtrations. This file also contains the one-sided martingale bound which
is required to prove the generalized Borel-Cantelli.

**Note**: the usual Borel-Cantelli lemmas are not in this file.
See `MeasureTheory.measure_limsup_atTop_eq_zero` for the first (which does not depend on
the results here), and `ProbabilityTheory.measure_limsup_eq_one` for the second (which does).

## Main results

- `MeasureTheory.Submartingale.bddAbove_iff_exists_tendsto`: the one-sided martingale bound: given
  a submartingale `f` with uniformly bounded differences, the set for which `f` converges is almost
  everywhere equal to the set for which it is bounded.
- `MeasureTheory.ae_mem_limsup_atTop_iff`: L√©vy's generalized Borel-Cantelli:
  given a filtration `‚Ñ±` and a sequence of sets `s` such that `s n ‚àà ‚Ñ± n` for all `n`,
  `limsup atTop s` is almost everywhere equal to the set for which `‚àë ‚Ñô[s (n + 1)‚à£‚Ñ± n] = ‚àû`.

-/

@[expose] public section


open Filter

open scoped NNReal ENNReal MeasureTheory ProbabilityTheory Topology

namespace MeasureTheory

variable {Œ© : Type*} {m0 : MeasurableSpace Œ©} {Œº : Measure Œ©} {‚Ñ± : Filtration ‚Ñï m0} {f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù}

/-!
### One-sided martingale bound
-/

/-- `leastGE f r` is the stopping time corresponding to the first time `f ‚â• r`. -/
noncomputable def leastGE (f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù) (r : ‚Ñù) : Œ© ‚Üí ‚Ñï‚àû :=
  hittingAfter f (Set.Ici r) 0

theorem StronglyAdapted.isStoppingTime_leastGE (r : ‚Ñù) (hf : StronglyAdapted ‚Ñ± f) :
    IsStoppingTime ‚Ñ± (leastGE f r) :=
  hittingAfter_isStoppingTime hf measurableSet_Ici

@[deprecated (since := "2025-12-19")]
alias Adapted.isStoppingTime_leastGE := StronglyAdapted.isStoppingTime_leastGE

/-- The stopped process of `f` above `r` is the process that is equal to `f` until `leastGE f r`
(the first time `f` passes above `r`), and then is constant afterwards. -/
noncomputable def stoppedAbove (f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù) (r : ‚Ñù) : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù :=
  stoppedProcess f (leastGE f r)

protected lemma Submartingale.stoppedAbove [IsFiniteMeasure Œº] (hf : Submartingale f ‚Ñ± Œº) (r : ‚Ñù) :
    Submartingale (stoppedAbove f r) ‚Ñ± Œº :=
  hf.stoppedProcess (hf.stronglyAdapted.isStoppingTime_leastGE r)

@[deprecated (since := "2025-10-25")] alias Submartingale.stoppedValue_leastGE :=
  Submartingale.stoppedAbove

variable {r : ‚Ñù} {R : ‚Ñù‚â•0}

theorem stoppedAbove_le (hr : 0 ‚â§ r) (hf0 : f 0 = 0)
    (hbdd : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ i, |f (i + 1) œâ - f i œâ| ‚â§ R) (i : ‚Ñï) :
    ‚àÄ·µê œâ ‚àÇŒº, stoppedAbove f r i œâ ‚â§ r + R := by
  filter_upwards [hbdd] with œâ hbddœâ
  rw [stoppedAbove, stoppedProcess, ENat.some_eq_coe]
  by_cases h_zero : (min (i : ‚Ñï‚àû) (leastGE f r œâ)).untopA = 0
  ¬∑ simp only [h_zero, hf0, Pi.zero_apply]
    positivity
  obtain ‚ü®k, hk‚ü© := Nat.exists_eq_add_one_of_ne_zero h_zero
  rw [hk, add_comm r, ‚Üê sub_le_iff_le_add]
  have := notMem_of_lt_hittingAfter (?_ : k < leastGE f r œâ)
  ¬∑ simp only [zero_le, Set.mem_Ici, not_le, forall_const] at this
    exact (sub_lt_sub_left this _).le.trans ((le_abs_self _).trans (hbddœâ _))
  ¬∑ suffices (k : ‚Ñï‚àû) < min (i : ‚Ñï‚àû) (leastGE f r œâ) from this.trans_le (min_le_right _ _)
    have h_top : min (i : ‚Ñï‚àû) (leastGE f r œâ) ‚â† ‚ä§ :=
      ne_top_of_le_ne_top (by simp) (min_le_left _ _)
    lift min (i : ‚Ñï‚àû) (leastGE f r œâ) to ‚Ñï using h_top with p
    simp only [untopD_coe_enat, Nat.cast_lt, gt_iff_lt] at *
    lia

@[deprecated (since := "2025-10-25")] alias norm_stoppedValue_leastGE_le := stoppedAbove_le

theorem Submartingale.eLpNorm_stoppedAbove_le [IsFiniteMeasure Œº] (hf : Submartingale f ‚Ñ± Œº)
    (hr : 0 ‚â§ r) (hf0 : f 0 = 0) (hbdd : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ i, |f (i + 1) œâ - f i œâ| ‚â§ R) (i : ‚Ñï) :
    eLpNorm (stoppedAbove f r i) 1 Œº ‚â§ 2 * Œº Set.univ * ENNReal.ofReal (r + R) := by
  refine eLpNorm_one_le_of_le' ((hf.stoppedAbove r).integrable _) ?_
    (stoppedAbove_le hr hf0 hbdd i)
  rw [‚Üê setIntegral_univ]
  refine le_trans ?_ ((hf.stoppedAbove r).setIntegral_le (zero_le _) MeasurableSet.univ)
  simp [stoppedAbove, stoppedProcess, hf0]

@[deprecated (since := "2025-10-25")] alias Submartingale.stoppedValue_leastGE_eLpNorm_le :=
  Submartingale.eLpNorm_stoppedAbove_le

theorem Submartingale.eLpNorm_stoppedAbove_le' [IsFiniteMeasure Œº]
    (hf : Submartingale f ‚Ñ± Œº) (hr : 0 ‚â§ r) (hf0 : f 0 = 0)
    (hbdd : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ i, |f (i + 1) œâ - f i œâ| ‚â§ R) (i : ‚Ñï) :
    eLpNorm (stoppedAbove f r i) 1 Œº
      ‚â§ ENNReal.toNNReal (2 * Œº Set.univ * ENNReal.ofReal (r + R)) := by
  refine (hf.eLpNorm_stoppedAbove_le hr hf0 hbdd i).trans ?_
  simp [ENNReal.coe_toNNReal (measure_ne_top Œº _), ENNReal.coe_toNNReal]

@[deprecated (since := "2025-10-25")] alias Submartingale.stoppedValue_leastGE_eLpNorm_le' :=
  Submartingale.eLpNorm_stoppedAbove_le'

/-- This lemma is superseded by `Submartingale.bddAbove_iff_exists_tendsto`. -/
theorem Submartingale.exists_tendsto_of_abs_bddAbove_aux [IsFiniteMeasure Œº]
    (hf : Submartingale f ‚Ñ± Œº) (hf0 : f 0 = 0) (hbdd : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ i, |f (i + 1) œâ - f i œâ| ‚â§ R) :
    ‚àÄ·µê œâ ‚àÇŒº, BddAbove (Set.range fun n => f n œâ) ‚Üí ‚àÉ c, Tendsto (fun n => f n œâ) atTop (ùìù c) := by
  have ht : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ i : ‚Ñï, ‚àÉ c, Tendsto (fun n => stoppedAbove f i n œâ) atTop (ùìù c) := by
    rw [ae_all_iff]
    exact fun i ‚Ü¶ Submartingale.exists_ae_tendsto_of_bdd (hf.stoppedAbove i)
      (hf.eLpNorm_stoppedAbove_le' i.cast_nonneg hf0 hbdd)
  filter_upwards [ht] with œâ hœâ hœâb
  rw [BddAbove] at hœâb
  obtain ‚ü®i, hi‚ü© := exists_nat_gt hœâb.some
  have hib : ‚àÄ n, f n œâ < i := by
    intro n
    exact lt_of_le_of_lt ((mem_upperBounds.1 hœâb.some_mem) _ ‚ü®n, rfl‚ü©) hi
  have heq : ‚àÄ n, stoppedAbove f i n œâ = f n œâ := by
    intro n
    rw [stoppedAbove, stoppedProcess, leastGE, hittingAfter_eq_top_iff.mpr]
    ¬∑ simp only [le_top, inf_of_le_left]
      congr
    ¬∑ simp [hib]
  simp only [‚Üê heq, hœâ i]

theorem Submartingale.bddAbove_iff_exists_tendsto_aux [IsFiniteMeasure Œº] (hf : Submartingale f ‚Ñ± Œº)
    (hf0 : f 0 = 0) (hbdd : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ i, |f (i + 1) œâ - f i œâ| ‚â§ R) :
    ‚àÄ·µê œâ ‚àÇŒº, BddAbove (Set.range fun n => f n œâ) ‚Üî ‚àÉ c, Tendsto (fun n => f n œâ) atTop (ùìù c) := by
  filter_upwards [hf.exists_tendsto_of_abs_bddAbove_aux hf0 hbdd] with œâ hœâ using
    ‚ü®hœâ, fun ‚ü®c, hc‚ü© => hc.bddAbove_range‚ü©

/-- One-sided martingale bound: If `f` is a submartingale which has uniformly bounded differences,
then for almost every `œâ`, `f n œâ` is bounded above (in `n`) if and only if it converges. -/
theorem Submartingale.bddAbove_iff_exists_tendsto [IsFiniteMeasure Œº] (hf : Submartingale f ‚Ñ± Œº)
    (hbdd : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ i, |f (i + 1) œâ - f i œâ| ‚â§ R) :
    ‚àÄ·µê œâ ‚àÇŒº, BddAbove (Set.range fun n => f n œâ) ‚Üî ‚àÉ c, Tendsto (fun n => f n œâ) atTop (ùìù c) := by
  set g : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù := fun n œâ => f n œâ - f 0 œâ
  have hg : Submartingale g ‚Ñ± Œº :=
    hf.sub_martingale (martingale_const_fun _ _ (hf.stronglyAdapted 0) (hf.integrable 0))
  have hg0 : g 0 = 0 := by
    ext œâ
    simp only [g, sub_self, Pi.zero_apply]
  have hgbdd : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ i : ‚Ñï, |g (i + 1) œâ - g i œâ| ‚â§ ‚ÜëR := by
    simpa only [g, sub_sub_sub_cancel_right]
  filter_upwards [hg.bddAbove_iff_exists_tendsto_aux hg0 hgbdd] with œâ hœâ
  convert hœâ using 1
  ¬∑ refine ‚ü®fun h => ?_, fun h => ?_‚ü© <;> obtain ‚ü®b, hb‚ü© := h <;>
    refine ‚ü®b + |f 0 œâ|, fun y hy => ?_‚ü© <;> obtain ‚ü®n, rfl‚ü© := hy
    ¬∑ simp_rw [g, sub_eq_add_neg]
      exact add_le_add (hb ‚ü®n, rfl‚ü©) (neg_le_abs _)
    ¬∑ exact sub_le_iff_le_add.1 (le_trans (sub_le_sub_left (le_abs_self _) _) (hb ‚ü®n, rfl‚ü©))
  ¬∑ refine ‚ü®fun h => ?_, fun h => ?_‚ü© <;> obtain ‚ü®c, hc‚ü© := h
    ¬∑ exact ‚ü®c - f 0 œâ, hc.sub_const _‚ü©
    ¬∑ refine ‚ü®c + f 0 œâ, ?_‚ü©
      have := hc.add_const (f 0 œâ)
      simpa only [g, sub_add_cancel]

/-!
### L√©vy's generalization of the Borel-Cantelli lemma

L√©vy's generalization of the Borel-Cantelli lemma states that: given a natural number indexed
filtration $(\mathcal{F}_n)$, and a sequence of sets $(s_n)$ such that for all
$n$, $s_n \in \mathcal{F}_n$, $limsup_n s_n$ is almost everywhere equal to the set for which
$\sum_n \mathbb{P}[s_n \mid \mathcal{F}_n] = \infty$.

The proof strategy follows by constructing a martingale satisfying the one-sided martingale bound.
In particular, we define
$$
  f_n := \sum_{k < n} \big(\mathbf{1}_{s_{k + 1}} - \mathbb{P}[s_{k + 1} \mid \mathcal{F}_k]\big).
$$
Then, as a martingale is both a sub- and a super-martingale, the set for which it is unbounded from
above must agree with the set for which it is unbounded from below almost everywhere. Thus, it
can only converge to $\pm \infty$ with probability 0. Thus, by considering
$$
  \limsup_n s_n = \{\sum_n \mathbf{1}_{s_n} = \infty\}
$$
almost everywhere, the result follows.
-/


theorem Martingale.bddAbove_range_iff_bddBelow_range [IsFiniteMeasure Œº] (hf : Martingale f ‚Ñ± Œº)
    (hbdd : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ i, |f (i + 1) œâ - f i œâ| ‚â§ R) :
    ‚àÄ·µê œâ ‚àÇŒº, BddAbove (Set.range fun n => f n œâ) ‚Üî BddBelow (Set.range fun n => f n œâ) := by
  have hbdd' : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ i, |(-f) (i + 1) œâ - (-f) i œâ| ‚â§ R := by
    filter_upwards [hbdd] with œâ hœâ i
    simp only [Pi.neg_apply]
    grind
  have hup := hf.submartingale.bddAbove_iff_exists_tendsto hbdd
  have hdown := hf.neg.submartingale.bddAbove_iff_exists_tendsto hbdd'
  filter_upwards [hup, hdown] with œâ hœâ‚ÇÅ hœâ‚ÇÇ
  have : (‚àÉ c, Tendsto (fun n => f n œâ) atTop (ùìù c)) ‚Üî
      ‚àÉ c, Tendsto (fun n => (-f) n œâ) atTop (ùìù c) := by
    constructor <;> rintro ‚ü®c, hc‚ü©
    ¬∑ exact ‚ü®-c, hc.neg‚ü©
    ¬∑ refine ‚ü®-c, ?_‚ü©
      convert hc.neg
      simp only [neg_neg, Pi.neg_apply]
  rw [hœâ‚ÇÅ, this, ‚Üê hœâ‚ÇÇ]
  constructor <;> rintro ‚ü®c, hc‚ü© <;> refine ‚ü®-c, fun œâ hœâ => ?_‚ü©
  ¬∑ rw [mem_upperBounds] at hc
    refine neg_le.2 (hc _ ?_)
    simpa only [Pi.neg_apply, Set.mem_range, neg_inj]
  ¬∑ rw [mem_lowerBounds] at hc
    simp_rw [Set.mem_range, Pi.neg_apply, neg_eq_iff_eq_neg] at hœâ
    refine le_neg.1 (hc _ ?_)
    simpa only [Set.mem_range]

theorem Martingale.ae_not_tendsto_atTop_atTop [IsFiniteMeasure Œº] (hf : Martingale f ‚Ñ± Œº)
    (hbdd : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ i, |f (i + 1) œâ - f i œâ| ‚â§ R) :
    ‚àÄ·µê œâ ‚àÇŒº, ¬¨Tendsto (fun n => f n œâ) atTop atTop := by
  filter_upwards [hf.bddAbove_range_iff_bddBelow_range hbdd] with œâ hœâ htop using
    not_bddAbove_of_tendsto_atTop htop (hœâ.2 <| bddBelow_range_of_tendsto_atTop_atTop htop)

theorem Martingale.ae_not_tendsto_atTop_atBot [IsFiniteMeasure Œº] (hf : Martingale f ‚Ñ± Œº)
    (hbdd : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ i, |f (i + 1) œâ - f i œâ| ‚â§ R) :
    ‚àÄ·µê œâ ‚àÇŒº, ¬¨Tendsto (fun n => f n œâ) atTop atBot := by
  filter_upwards [hf.bddAbove_range_iff_bddBelow_range hbdd] with œâ hœâ htop using
    not_bddBelow_of_tendsto_atBot htop (hœâ.1 <| bddAbove_range_of_tendsto_atTop_atBot htop)

namespace BorelCantelli

/-- Auxiliary definition required to prove L√©vy's generalization of the Borel-Cantelli lemmas for
which we will take the martingale part. -/
noncomputable def process (s : ‚Ñï ‚Üí Set Œ©) (n : ‚Ñï) : Œ© ‚Üí ‚Ñù :=
  ‚àë k ‚àà Finset.range n, (s (k + 1)).indicator 1

variable {s : ‚Ñï ‚Üí Set Œ©}

theorem process_zero : process s 0 = 0 := by rw [process, Finset.range_zero, Finset.sum_empty]

theorem stronglyAdapted_process (hs : ‚àÄ n, MeasurableSet[‚Ñ± n] (s n)) :
    StronglyAdapted ‚Ñ± (process s) :=
  fun _ => Finset.stronglyMeasurable_sum _ fun _ hk =>
    stronglyMeasurable_one.indicator <| ‚Ñ±.mono (Finset.mem_range.1 hk) _ <| hs _

@[deprecated (since := "2025-12-19")]
alias adapted_process := stronglyAdapted_process

theorem martingalePart_process_ae_eq (‚Ñ± : Filtration ‚Ñï m0) (Œº : Measure Œ©) (s : ‚Ñï ‚Üí Set Œ©) (n : ‚Ñï) :
    martingalePart (process s) ‚Ñ± Œº n =
      ‚àë k ‚àà Finset.range n, ((s (k + 1)).indicator 1 - Œº[(s (k + 1)).indicator 1|‚Ñ± k]) := by
  simp only [martingalePart_eq_sum, process_zero, zero_add]
  refine Finset.sum_congr rfl fun k _ => ?_
  simp only [process, Finset.sum_range_succ_sub_sum]

theorem predictablePart_process_ae_eq (‚Ñ± : Filtration ‚Ñï m0) (Œº : Measure Œ©) (s : ‚Ñï ‚Üí Set Œ©)
    (n : ‚Ñï) : predictablePart (process s) ‚Ñ± Œº n =
    ‚àë k ‚àà Finset.range n, Œº[(s (k + 1)).indicator (1 : Œ© ‚Üí ‚Ñù)|‚Ñ± k] := by
  have := martingalePart_process_ae_eq ‚Ñ± Œº s n
  simp_rw [martingalePart, process, Finset.sum_sub_distrib] at this
  exact sub_right_injective this

theorem process_difference_le (s : ‚Ñï ‚Üí Set Œ©) (œâ : Œ©) (n : ‚Ñï) :
    |process s (n + 1) œâ - process s n œâ| ‚â§ (1 : ‚Ñù‚â•0) := by
  norm_cast
  rw [process, process, Finset.sum_apply, Finset.sum_apply,
    Finset.sum_range_succ_sub_sum, ‚Üê Real.norm_eq_abs, norm_indicator_eq_indicator_norm]
  refine Set.indicator_le' (fun _ _ => ?_) (fun _ _ => zero_le_one) _
  rw [Pi.one_apply, norm_one]

theorem integrable_process (Œº : Measure Œ©) [IsFiniteMeasure Œº] (hs : ‚àÄ n, MeasurableSet[‚Ñ± n] (s n))
    (n : ‚Ñï) : Integrable (process s n) Œº :=
  integrable_finset_sum' _ fun _ _ =>
    IntegrableOn.integrable_indicator (integrable_const 1) <| ‚Ñ±.le _ _ <| hs _

end BorelCantelli

open BorelCantelli

/-- An a.e. monotone strongly adapted process `f` with uniformly bounded differences converges to
`+‚àû` if and only if its predictable part also converges to `+‚àû`. -/
theorem tendsto_sum_indicator_atTop_iff [IsFiniteMeasure Œº]
    (hfmono : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ n, f n œâ ‚â§ f (n + 1) œâ) (hf : StronglyAdapted ‚Ñ± f)
    (hint : ‚àÄ n, Integrable (f n) Œº) (hbdd : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ n, |f (n + 1) œâ - f n œâ| ‚â§ R) :
    ‚àÄ·µê œâ ‚àÇŒº, Tendsto (fun n => f n œâ) atTop atTop ‚Üî
      Tendsto (fun n => predictablePart f ‚Ñ± Œº n œâ) atTop atTop := by
  have h‚ÇÅ := (martingale_martingalePart hf hint).ae_not_tendsto_atTop_atTop
    (martingalePart_bdd_difference ‚Ñ± hbdd)
  have h‚ÇÇ := (martingale_martingalePart hf hint).ae_not_tendsto_atTop_atBot
    (martingalePart_bdd_difference ‚Ñ± hbdd)
  have h‚ÇÉ : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ n, 0 ‚â§ (Œº[f (n + 1) - f n|‚Ñ± n]) œâ := by
    refine ae_all_iff.2 fun n => condExp_nonneg ?_
    filter_upwards [ae_all_iff.1 hfmono n] with œâ hœâ using sub_nonneg.2 hœâ
  filter_upwards [h‚ÇÅ, h‚ÇÇ, h‚ÇÉ, hfmono] with œâ hœâ‚ÇÅ hœâ‚ÇÇ hœâ‚ÇÉ hœâ‚ÇÑ
  constructor <;> intro ht
  ¬∑ refine tendsto_atTop_atTop_of_monotone' ?_ ?_
    ¬∑ intro n m hnm
      simp only [predictablePart, Finset.sum_apply]
      exact Finset.sum_mono_set_of_nonneg hœâ‚ÇÉ (Finset.range_mono hnm)
    rintro ‚ü®b, hbdd‚ü©
    rw [‚Üê tendsto_neg_atBot_iff] at ht
    simp only [martingalePart, sub_eq_add_neg] at hœâ‚ÇÅ
    exact hœâ‚ÇÅ (tendsto_atTop_add_right_of_le _ (-b) (tendsto_neg_atBot_iff.1 ht) fun n =>
      neg_le_neg (hbdd ‚ü®n, rfl‚ü©))
  ¬∑ refine tendsto_atTop_atTop_of_monotone' (monotone_nat_of_le_succ hœâ‚ÇÑ) ?_
    rintro ‚ü®b, hbdd‚ü©
    exact hœâ‚ÇÇ ((tendsto_atBot_add_left_of_ge _ b fun n =>
      hbdd ‚ü®n, rfl‚ü©) <| tendsto_neg_atBot_iff.2 ht)

open BorelCantelli

theorem tendsto_sum_indicator_atTop_iff' [IsFiniteMeasure Œº] {s : ‚Ñï ‚Üí Set Œ©}
    (hs : ‚àÄ n, MeasurableSet[‚Ñ± n] (s n)) : ‚àÄ·µê œâ ‚àÇŒº,
    Tendsto (fun n => ‚àë k ‚àà Finset.range n,
      (s (k + 1)).indicator (1 : Œ© ‚Üí ‚Ñù) œâ) atTop atTop ‚Üî
    Tendsto (fun n => ‚àë k ‚àà Finset.range n,
      (Œº[(s (k + 1)).indicator (1 : Œ© ‚Üí ‚Ñù)|‚Ñ± k]) œâ) atTop atTop := by
  have := tendsto_sum_indicator_atTop_iff (Eventually.of_forall fun œâ n => ?_)
    (stronglyAdapted_process hs) (integrable_process Œº hs)
    (Eventually.of_forall <| process_difference_le s)
  swap
  ¬∑ rw [process, process, ‚Üê sub_nonneg, Finset.sum_apply, Finset.sum_apply,
      Finset.sum_range_succ_sub_sum]
    exact Set.indicator_nonneg (fun _ _ => zero_le_one) _
  simp_rw [process, predictablePart_process_ae_eq] at this
  simpa using this

/-- **L√©vy's generalization of the Borel-Cantelli lemma**: given a sequence of sets `s` and a
filtration `‚Ñ±` such that for all `n`, `s n` is `‚Ñ± n`-measurable, `limsup s atTop` is almost
everywhere equal to the set for which `‚àë k, ‚Ñô(s (k + 1) | ‚Ñ± k) = ‚àû`. -/
theorem ae_mem_limsup_atTop_iff (Œº : Measure Œ©) [IsFiniteMeasure Œº] {s : ‚Ñï ‚Üí Set Œ©}
    (hs : ‚àÄ n, MeasurableSet[‚Ñ± n] (s n)) : ‚àÄ·µê œâ ‚àÇŒº, œâ ‚àà limsup s atTop ‚Üî
    Tendsto (fun n => ‚àë k ‚àà Finset.range n,
      (Œº[(s (k + 1)).indicator (1 : Œ© ‚Üí ‚Ñù)|‚Ñ± k]) œâ) atTop atTop := by
  rw [‚Üê limsup_nat_add s 1,
    Set.limsup_eq_tendsto_sum_indicator_atTop (zero_lt_one (Œ± := ‚Ñù)) (fun n ‚Ü¶ s (n + 1))]
  exact tendsto_sum_indicator_atTop_iff' hs

end MeasureTheory
