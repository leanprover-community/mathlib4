/-
Copyright (c) 2022 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import Mathlib.Algebra.Order.Archimedean.IndicatorCard
import Mathlib.Probability.Martingale.Centering
import Mathlib.Probability.Martingale.Convergence
import Mathlib.Probability.Martingale.OptionalStopping

/-!

# Generalized Borel-Cantelli lemma

This file proves LÃ©vy's generalized Borel-Cantelli lemma which is a generalization of the
Borel-Cantelli lemmas. With this generalization, one can easily deduce the Borel-Cantelli lemmas
by choosing appropriate filtrations. This file also contains the one sided martingale bound which
is required to prove the generalized Borel-Cantelli.

**Note**: the usual Borel-Cantelli lemmas are not in this file.
See `MeasureTheory.measure_limsup_atTop_eq_zero` for the first (which does not depend on
the results here), and `ProbabilityTheory.measure_limsup_eq_one` for the second (which does).

## Main results

- `MeasureTheory.Submartingale.bddAbove_iff_exists_tendsto`: the one sided martingale bound: given
  a submartingale `f` with uniformly bounded differences, the set for which `f` converges is almost
  everywhere equal to the set for which it is bounded.
- `MeasureTheory.ae_mem_limsup_atTop_iff`: LÃ©vy's generalized Borel-Cantelli:
  given a filtration `â„±` and a sequence of sets `s` such that `s n âˆˆ â„± n` for all `n`,
  `limsup atTop s` is almost everywhere equal to the set for which `âˆ‘ â„™[s (n + 1)âˆ£â„± n] = âˆ`.

-/


open Filter

open scoped NNReal ENNReal MeasureTheory ProbabilityTheory Topology

namespace MeasureTheory

variable {Î© : Type*} {m0 : MeasurableSpace Î©} {Î¼ : Measure Î©} {â„± : Filtration â„• m0} {f : â„• â†’ Î© â†’ â„}

/-!
### One sided martingale bound
-/


-- TODO: `leastGE` should be defined taking values in `WithTop â„•` once the `stoppedProcess`
-- refactor is complete
/-- `leastGE f r n` is the stopping time corresponding to the first time `f â‰¥ r`. -/
noncomputable def leastGE (f : â„• â†’ Î© â†’ â„) (r : â„) (n : â„•) :=
  hitting f (Set.Ici r) 0 n

theorem Adapted.isStoppingTime_leastGE (r : â„) (n : â„•) (hf : Adapted â„± f) :
    IsStoppingTime â„± (leastGE f r n) :=
  hitting_isStoppingTime hf measurableSet_Ici

theorem leastGE_le {i : â„•} {r : â„} (Ï‰ : Î©) : leastGE f r i Ï‰ â‰¤ i :=
  hitting_le Ï‰

-- The following four lemmas shows `leastGE` behaves like a stopped process. Ideally we should
-- define `leastGE` as a stopping time and take its stopped process. However, we can't do that
-- with our current definition since a stopping time takes only finite indices. An upcoming
-- refactor should hopefully make it possible to have stopping times taking infinity as a value
theorem leastGE_mono {n m : â„•} (hnm : n â‰¤ m) (r : â„) (Ï‰ : Î©) : leastGE f r n Ï‰ â‰¤ leastGE f r m Ï‰ :=
  hitting_mono hnm

theorem leastGE_eq_min (Ï€ : Î© â†’ â„•) (r : â„) (Ï‰ : Î©) {n : â„•} (hÏ€n : âˆ€ Ï‰, Ï€ Ï‰ â‰¤ n) :
    leastGE f r (Ï€ Ï‰) Ï‰ = min (Ï€ Ï‰) (leastGE f r n Ï‰) := by
  classical
  refine le_antisymm (le_min (leastGE_le _) (leastGE_mono (hÏ€n Ï‰) r Ï‰)) ?_
  by_cases hle : Ï€ Ï‰ â‰¤ leastGE f r n Ï‰
  Â· rw [min_eq_left hle, leastGE]
    by_cases h : âˆƒ j âˆˆ Set.Icc 0 (Ï€ Ï‰), f j Ï‰ âˆˆ Set.Ici r
    Â· refine hle.trans (Eq.le ?_)
      rw [leastGE, â† hitting_eq_hitting_of_exists (hÏ€n Ï‰) h]
    Â· simp only [hitting, if_neg h, le_rfl]
  Â· rw [min_eq_right (not_le.1 hle).le, leastGE, leastGE, â†
      hitting_eq_hitting_of_exists (hÏ€n Ï‰) _]
    rw [not_le, leastGE, hitting_lt_iff _ (hÏ€n Ï‰)] at hle
    exact
      let âŸ¨j, hjâ‚, hjâ‚‚âŸ© := hle
      âŸ¨j, âŸ¨hjâ‚.1, hjâ‚.2.leâŸ©, hjâ‚‚âŸ©

theorem stoppedValue_stoppedValue_leastGE (f : â„• â†’ Î© â†’ â„) (Ï€ : Î© â†’ â„•) (r : â„) {n : â„•}
    (hÏ€n : âˆ€ Ï‰, Ï€ Ï‰ â‰¤ n) : stoppedValue (fun i => stoppedValue f (leastGE f r i)) Ï€ =
      stoppedValue (stoppedProcess f (leastGE f r n)) Ï€ := by
  ext1 Ï‰
  simp +unfoldPartialApp only [stoppedProcess, stoppedValue]
  rw [leastGE_eq_min _ _ _ hÏ€n]

theorem Submartingale.stoppedValue_leastGE [IsFiniteMeasure Î¼] (hf : Submartingale f â„± Î¼) (r : â„) :
    Submartingale (fun i => stoppedValue f (leastGE f r i)) â„± Î¼ := by
  rw [submartingale_iff_expected_stoppedValue_mono]
  Â· intro Ïƒ Ï€ hÏƒ hÏ€ hÏƒ_le_Ï€ hÏ€_bdd
    obtain âŸ¨n, hÏ€_le_nâŸ© := hÏ€_bdd
    simp_rw [stoppedValue_stoppedValue_leastGE f Ïƒ r fun i => (hÏƒ_le_Ï€ i).trans (hÏ€_le_n i)]
    simp_rw [stoppedValue_stoppedValue_leastGE f Ï€ r hÏ€_le_n]
    refine hf.expected_stoppedValue_mono ?_ ?_ ?_ fun Ï‰ => (min_le_left _ _).trans (hÏ€_le_n Ï‰)
    Â· exact hÏƒ.min (hf.adapted.isStoppingTime_leastGE _ _)
    Â· exact hÏ€.min (hf.adapted.isStoppingTime_leastGE _ _)
    Â· exact fun Ï‰ => min_le_min (hÏƒ_le_Ï€ Ï‰) le_rfl
  Â· exact fun i => stronglyMeasurable_stoppedValue_of_le hf.adapted.progMeasurable_of_discrete
      (hf.adapted.isStoppingTime_leastGE _ _) leastGE_le
  Â· exact fun i => integrable_stoppedValue _ (hf.adapted.isStoppingTime_leastGE _ _) hf.integrable
      leastGE_le

variable {r : â„} {R : â„â‰¥0}

theorem norm_stoppedValue_leastGE_le (hr : 0 â‰¤ r) (hf0 : f 0 = 0)
    (hbdd : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ i, |f (i + 1) Ï‰ - f i Ï‰| â‰¤ R) (i : â„•) :
    âˆ€áµ Ï‰ âˆ‚Î¼, stoppedValue f (leastGE f r i) Ï‰ â‰¤ r + R := by
  filter_upwards [hbdd] with Ï‰ hbddÏ‰
  change f (leastGE f r i Ï‰) Ï‰ â‰¤ r + R
  by_cases heq : leastGE f r i Ï‰ = 0
  Â· rw [heq, hf0, Pi.zero_apply]
    exact add_nonneg hr R.coe_nonneg
  Â· obtain âŸ¨k, hkâŸ© := Nat.exists_eq_succ_of_ne_zero heq
    rw [hk, add_comm, â† sub_le_iff_le_add]
    have := notMem_of_lt_hitting (hk.symm â–¸ k.lt_succ_self : k < leastGE f r i Ï‰) (zero_le _)
    simp only [Set.mem_Ici, not_le] at this
    exact (sub_lt_sub_left this _).le.trans ((le_abs_self _).trans (hbddÏ‰ _))

theorem Submartingale.stoppedValue_leastGE_eLpNorm_le [IsFiniteMeasure Î¼] (hf : Submartingale f â„± Î¼)
    (hr : 0 â‰¤ r) (hf0 : f 0 = 0) (hbdd : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ i, |f (i + 1) Ï‰ - f i Ï‰| â‰¤ R) (i : â„•) :
    eLpNorm (stoppedValue f (leastGE f r i)) 1 Î¼ â‰¤ 2 * Î¼ Set.univ * ENNReal.ofReal (r + R) := by
  refine eLpNorm_one_le_of_le' ((hf.stoppedValue_leastGE r).integrable _) ?_
    (norm_stoppedValue_leastGE_le hr hf0 hbdd i)
  rw [â† setIntegral_univ]
  refine le_trans ?_ ((hf.stoppedValue_leastGE r).setIntegral_le (zero_le _) MeasurableSet.univ)
  simp_rw [stoppedValue, leastGE, hitting_of_le le_rfl, hf0, integral_zero', le_rfl]

theorem Submartingale.stoppedValue_leastGE_eLpNorm_le' [IsFiniteMeasure Î¼]
    (hf : Submartingale f â„± Î¼) (hr : 0 â‰¤ r) (hf0 : f 0 = 0)
    (hbdd : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ i, |f (i + 1) Ï‰ - f i Ï‰| â‰¤ R) (i : â„•) :
    eLpNorm (stoppedValue f (leastGE f r i)) 1 Î¼ â‰¤
      ENNReal.toNNReal (2 * Î¼ Set.univ * ENNReal.ofReal (r + R)) := by
  refine (hf.stoppedValue_leastGE_eLpNorm_le hr hf0 hbdd i).trans ?_
  simp [ENNReal.coe_toNNReal (measure_ne_top Î¼ _), ENNReal.coe_toNNReal]

/-- This lemma is superseded by `Submartingale.bddAbove_iff_exists_tendsto`. -/
theorem Submartingale.exists_tendsto_of_abs_bddAbove_aux [IsFiniteMeasure Î¼]
    (hf : Submartingale f â„± Î¼) (hf0 : f 0 = 0) (hbdd : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ i, |f (i + 1) Ï‰ - f i Ï‰| â‰¤ R) :
    âˆ€áµ Ï‰ âˆ‚Î¼, BddAbove (Set.range fun n => f n Ï‰) â†’ âˆƒ c, Tendsto (fun n => f n Ï‰) atTop (ğ“ c) := by
  have ht :
    âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ i : â„•, âˆƒ c, Tendsto (fun n => stoppedValue f (leastGE f i n) Ï‰) atTop (ğ“ c) := by
    rw [ae_all_iff]
    exact fun i => Submartingale.exists_ae_tendsto_of_bdd (hf.stoppedValue_leastGE i)
      (hf.stoppedValue_leastGE_eLpNorm_le' i.cast_nonneg hf0 hbdd)
  filter_upwards [ht] with Ï‰ hÏ‰ hÏ‰b
  rw [BddAbove] at hÏ‰b
  obtain âŸ¨i, hiâŸ© := exists_nat_gt hÏ‰b.some
  have hib : âˆ€ n, f n Ï‰ < i := by
    intro n
    exact lt_of_le_of_lt ((mem_upperBounds.1 hÏ‰b.some_mem) _ âŸ¨n, rflâŸ©) hi
  have heq : âˆ€ n, stoppedValue f (leastGE f i n) Ï‰ = f n Ï‰ := by
    intro n
    rw [leastGE]; unfold hitting; rw [stoppedValue]
    rw [if_neg]
    simp only [Set.mem_Icc, Set.mem_Ici]
    push_neg
    exact fun j _ => hib j
  simp only [â† heq, hÏ‰ i]

theorem Submartingale.bddAbove_iff_exists_tendsto_aux [IsFiniteMeasure Î¼] (hf : Submartingale f â„± Î¼)
    (hf0 : f 0 = 0) (hbdd : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ i, |f (i + 1) Ï‰ - f i Ï‰| â‰¤ R) :
    âˆ€áµ Ï‰ âˆ‚Î¼, BddAbove (Set.range fun n => f n Ï‰) â†” âˆƒ c, Tendsto (fun n => f n Ï‰) atTop (ğ“ c) := by
  filter_upwards [hf.exists_tendsto_of_abs_bddAbove_aux hf0 hbdd] with Ï‰ hÏ‰ using
    âŸ¨hÏ‰, fun âŸ¨c, hcâŸ© => hc.bddAbove_rangeâŸ©

/-- One sided martingale bound: If `f` is a submartingale which has uniformly bounded differences,
then for almost every `Ï‰`, `f n Ï‰` is bounded above (in `n`) if and only if it converges. -/
theorem Submartingale.bddAbove_iff_exists_tendsto [IsFiniteMeasure Î¼] (hf : Submartingale f â„± Î¼)
    (hbdd : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ i, |f (i + 1) Ï‰ - f i Ï‰| â‰¤ R) :
    âˆ€áµ Ï‰ âˆ‚Î¼, BddAbove (Set.range fun n => f n Ï‰) â†” âˆƒ c, Tendsto (fun n => f n Ï‰) atTop (ğ“ c) := by
  set g : â„• â†’ Î© â†’ â„ := fun n Ï‰ => f n Ï‰ - f 0 Ï‰
  have hg : Submartingale g â„± Î¼ :=
    hf.sub_martingale (martingale_const_fun _ _ (hf.adapted 0) (hf.integrable 0))
  have hg0 : g 0 = 0 := by
    ext Ï‰
    simp only [g, sub_self, Pi.zero_apply]
  have hgbdd : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ i : â„•, |g (i + 1) Ï‰ - g i Ï‰| â‰¤ â†‘R := by
    simpa only [g, sub_sub_sub_cancel_right]
  filter_upwards [hg.bddAbove_iff_exists_tendsto_aux hg0 hgbdd] with Ï‰ hÏ‰
  convert hÏ‰ using 1
  Â· refine âŸ¨fun h => ?_, fun h => ?_âŸ© <;> obtain âŸ¨b, hbâŸ© := h <;>
    refine âŸ¨b + |f 0 Ï‰|, fun y hy => ?_âŸ© <;> obtain âŸ¨n, rflâŸ© := hy
    Â· simp_rw [g, sub_eq_add_neg]
      exact add_le_add (hb âŸ¨n, rflâŸ©) (neg_le_abs _)
    Â· exact sub_le_iff_le_add.1 (le_trans (sub_le_sub_left (le_abs_self _) _) (hb âŸ¨n, rflâŸ©))
  Â· refine âŸ¨fun h => ?_, fun h => ?_âŸ© <;> obtain âŸ¨c, hcâŸ© := h
    Â· exact âŸ¨c - f 0 Ï‰, hc.sub_const _âŸ©
    Â· refine âŸ¨c + f 0 Ï‰, ?_âŸ©
      have := hc.add_const (f 0 Ï‰)
      simpa only [g, sub_add_cancel]

/-!
### LÃ©vy's generalization of the Borel-Cantelli lemma

LÃ©vy's generalization of the Borel-Cantelli lemma states that: given a natural number indexed
filtration $(\mathcal{F}_n)$, and a sequence of sets $(s_n)$ such that for all
$n$, $s_n \in \mathcal{F}_n$, $limsup_n s_n$ is almost everywhere equal to the set for which
$\sum_n \mathbb{P}[s_n \mid \mathcal{F}_n] = \infty$.

The proof strategy follows by constructing a martingale satisfying the one sided martingale bound.
In particular, we define
$$
  f_n := \sum_{k < n} \mathbf{1}_{s_{n + 1}} - \mathbb{P}[s_{n + 1} \mid \mathcal{F}_n].
$$
Then, as a martingale is both a sub and a super-martingale, the set for which it is unbounded from
above must agree with the set for which it is unbounded from below almost everywhere. Thus, it
can only converge to $\pm \infty$ with probability 0. Thus, by considering
$$
  \limsup_n s_n = \{\sum_n \mathbf{1}_{s_n} = \infty\}
$$
almost everywhere, the result follows.
-/


theorem Martingale.bddAbove_range_iff_bddBelow_range [IsFiniteMeasure Î¼] (hf : Martingale f â„± Î¼)
    (hbdd : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ i, |f (i + 1) Ï‰ - f i Ï‰| â‰¤ R) :
    âˆ€áµ Ï‰ âˆ‚Î¼, BddAbove (Set.range fun n => f n Ï‰) â†” BddBelow (Set.range fun n => f n Ï‰) := by
  have hbdd' : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ i, |(-f) (i + 1) Ï‰ - (-f) i Ï‰| â‰¤ R := by
    filter_upwards [hbdd] with Ï‰ hÏ‰ i
    erw [â† abs_neg, neg_sub, sub_neg_eq_add, neg_add_eq_sub]
    exact hÏ‰ i
  have hup := hf.submartingale.bddAbove_iff_exists_tendsto hbdd
  have hdown := hf.neg.submartingale.bddAbove_iff_exists_tendsto hbdd'
  filter_upwards [hup, hdown] with Ï‰ hÏ‰â‚ hÏ‰â‚‚
  have : (âˆƒ c, Tendsto (fun n => f n Ï‰) atTop (ğ“ c)) â†”
      âˆƒ c, Tendsto (fun n => (-f) n Ï‰) atTop (ğ“ c) := by
    constructor <;> rintro âŸ¨c, hcâŸ©
    Â· exact âŸ¨-c, hc.negâŸ©
    Â· refine âŸ¨-c, ?_âŸ©
      convert hc.neg
      simp only [neg_neg, Pi.neg_apply]
  rw [hÏ‰â‚, this, â† hÏ‰â‚‚]
  constructor <;> rintro âŸ¨c, hcâŸ© <;> refine âŸ¨-c, fun Ï‰ hÏ‰ => ?_âŸ©
  Â· rw [mem_upperBounds] at hc
    refine neg_le.2 (hc _ ?_)
    simpa only [Pi.neg_apply, Set.mem_range, neg_inj]
  Â· rw [mem_lowerBounds] at hc
    simp_rw [Set.mem_range, Pi.neg_apply, neg_eq_iff_eq_neg] at hÏ‰
    refine le_neg.1 (hc _ ?_)
    simpa only [Set.mem_range]

theorem Martingale.ae_not_tendsto_atTop_atTop [IsFiniteMeasure Î¼] (hf : Martingale f â„± Î¼)
    (hbdd : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ i, |f (i + 1) Ï‰ - f i Ï‰| â‰¤ R) :
    âˆ€áµ Ï‰ âˆ‚Î¼, Â¬Tendsto (fun n => f n Ï‰) atTop atTop := by
  filter_upwards [hf.bddAbove_range_iff_bddBelow_range hbdd] with Ï‰ hÏ‰ htop using
    not_bddAbove_of_tendsto_atTop htop (hÏ‰.2 <| bddBelow_range_of_tendsto_atTop_atTop htop)

theorem Martingale.ae_not_tendsto_atTop_atBot [IsFiniteMeasure Î¼] (hf : Martingale f â„± Î¼)
    (hbdd : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ i, |f (i + 1) Ï‰ - f i Ï‰| â‰¤ R) :
    âˆ€áµ Ï‰ âˆ‚Î¼, Â¬Tendsto (fun n => f n Ï‰) atTop atBot := by
  filter_upwards [hf.bddAbove_range_iff_bddBelow_range hbdd] with Ï‰ hÏ‰ htop using
    not_bddBelow_of_tendsto_atBot htop (hÏ‰.1 <| bddAbove_range_of_tendsto_atTop_atBot htop)

namespace BorelCantelli

/-- Auxiliary definition required to prove LÃ©vy's generalization of the Borel-Cantelli lemmas for
which we will take the martingale part. -/
noncomputable def process (s : â„• â†’ Set Î©) (n : â„•) : Î© â†’ â„ :=
  âˆ‘ k âˆˆ Finset.range n, (s (k + 1)).indicator 1

variable {s : â„• â†’ Set Î©}

theorem process_zero : process s 0 = 0 := by rw [process, Finset.range_zero, Finset.sum_empty]

theorem adapted_process (hs : âˆ€ n, MeasurableSet[â„± n] (s n)) : Adapted â„± (process s) := fun _ =>
  Finset.stronglyMeasurable_sum _ fun _ hk =>
    stronglyMeasurable_one.indicator <| â„±.mono (Finset.mem_range.1 hk) _ <| hs _

theorem martingalePart_process_ae_eq (â„± : Filtration â„• m0) (Î¼ : Measure Î©) (s : â„• â†’ Set Î©) (n : â„•) :
    martingalePart (process s) â„± Î¼ n =
      âˆ‘ k âˆˆ Finset.range n, ((s (k + 1)).indicator 1 - Î¼[(s (k + 1)).indicator 1|â„± k]) := by
  simp only [martingalePart_eq_sum, process_zero, zero_add]
  refine Finset.sum_congr rfl fun k _ => ?_
  simp only [process, Finset.sum_range_succ_sub_sum]

theorem predictablePart_process_ae_eq (â„± : Filtration â„• m0) (Î¼ : Measure Î©) (s : â„• â†’ Set Î©)
    (n : â„•) : predictablePart (process s) â„± Î¼ n =
    âˆ‘ k âˆˆ Finset.range n, Î¼[(s (k + 1)).indicator (1 : Î© â†’ â„)|â„± k] := by
  have := martingalePart_process_ae_eq â„± Î¼ s n
  simp_rw [martingalePart, process, Finset.sum_sub_distrib] at this
  exact sub_right_injective this

theorem process_difference_le (s : â„• â†’ Set Î©) (Ï‰ : Î©) (n : â„•) :
    |process s (n + 1) Ï‰ - process s n Ï‰| â‰¤ (1 : â„â‰¥0) := by
  norm_cast
  rw [process, process, Finset.sum_apply, Finset.sum_apply,
    Finset.sum_range_succ_sub_sum, â† Real.norm_eq_abs, norm_indicator_eq_indicator_norm]
  refine Set.indicator_le' (fun _ _ => ?_) (fun _ _ => zero_le_one) _
  rw [Pi.one_apply, norm_one]

theorem integrable_process (Î¼ : Measure Î©) [IsFiniteMeasure Î¼] (hs : âˆ€ n, MeasurableSet[â„± n] (s n))
    (n : â„•) : Integrable (process s n) Î¼ :=
  integrable_finset_sum' _ fun _ _ =>
    IntegrableOn.integrable_indicator (integrable_const 1) <| â„±.le _ _ <| hs _

end BorelCantelli

open BorelCantelli

/-- An a.e. monotone adapted process `f` with uniformly bounded differences converges to `+âˆ` if
and only if its predictable part also converges to `+âˆ`. -/
theorem tendsto_sum_indicator_atTop_iff [IsFiniteMeasure Î¼]
    (hfmono : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ n, f n Ï‰ â‰¤ f (n + 1) Ï‰) (hf : Adapted â„± f) (hint : âˆ€ n, Integrable (f n) Î¼)
    (hbdd : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ n, |f (n + 1) Ï‰ - f n Ï‰| â‰¤ R) :
    âˆ€áµ Ï‰ âˆ‚Î¼, Tendsto (fun n => f n Ï‰) atTop atTop â†”
      Tendsto (fun n => predictablePart f â„± Î¼ n Ï‰) atTop atTop := by
  have hâ‚ := (martingale_martingalePart hf hint).ae_not_tendsto_atTop_atTop
    (martingalePart_bdd_difference â„± hbdd)
  have hâ‚‚ := (martingale_martingalePart hf hint).ae_not_tendsto_atTop_atBot
    (martingalePart_bdd_difference â„± hbdd)
  have hâ‚ƒ : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ n, 0 â‰¤ (Î¼[f (n + 1) - f n|â„± n]) Ï‰ := by
    refine ae_all_iff.2 fun n => condExp_nonneg ?_
    filter_upwards [ae_all_iff.1 hfmono n] with Ï‰ hÏ‰ using sub_nonneg.2 hÏ‰
  filter_upwards [hâ‚, hâ‚‚, hâ‚ƒ, hfmono] with Ï‰ hÏ‰â‚ hÏ‰â‚‚ hÏ‰â‚ƒ hÏ‰â‚„
  constructor <;> intro ht
  Â· refine tendsto_atTop_atTop_of_monotone' ?_ ?_
    Â· intro n m hnm
      simp only [predictablePart, Finset.sum_apply]
      exact Finset.sum_mono_set_of_nonneg hÏ‰â‚ƒ (Finset.range_mono hnm)
    rintro âŸ¨b, hbddâŸ©
    rw [â† tendsto_neg_atBot_iff] at ht
    simp only [martingalePart, sub_eq_add_neg] at hÏ‰â‚
    exact hÏ‰â‚ (tendsto_atTop_add_right_of_le _ (-b) (tendsto_neg_atBot_iff.1 ht) fun n =>
      neg_le_neg (hbdd âŸ¨n, rflâŸ©))
  Â· refine tendsto_atTop_atTop_of_monotone' (monotone_nat_of_le_succ hÏ‰â‚„) ?_
    rintro âŸ¨b, hbddâŸ©
    exact hÏ‰â‚‚ ((tendsto_atBot_add_left_of_ge _ b fun n =>
      hbdd âŸ¨n, rflâŸ©) <| tendsto_neg_atBot_iff.2 ht)

open BorelCantelli

theorem tendsto_sum_indicator_atTop_iff' [IsFiniteMeasure Î¼] {s : â„• â†’ Set Î©}
    (hs : âˆ€ n, MeasurableSet[â„± n] (s n)) : âˆ€áµ Ï‰ âˆ‚Î¼,
    Tendsto (fun n => âˆ‘ k âˆˆ Finset.range n,
      (s (k + 1)).indicator (1 : Î© â†’ â„) Ï‰) atTop atTop â†”
    Tendsto (fun n => âˆ‘ k âˆˆ Finset.range n,
      (Î¼[(s (k + 1)).indicator (1 : Î© â†’ â„)|â„± k]) Ï‰) atTop atTop := by
  have := tendsto_sum_indicator_atTop_iff (Eventually.of_forall fun Ï‰ n => ?_) (adapted_process hs)
    (integrable_process Î¼ hs) (Eventually.of_forall <| process_difference_le s)
  swap
  Â· rw [process, process, â† sub_nonneg, Finset.sum_apply, Finset.sum_apply,
      Finset.sum_range_succ_sub_sum]
    exact Set.indicator_nonneg (fun _ _ => zero_le_one) _
  simp_rw [process, predictablePart_process_ae_eq] at this
  simpa using this

/-- **LÃ©vy's generalization of the Borel-Cantelli lemma**: given a sequence of sets `s` and a
filtration `â„±` such that for all `n`, `s n` is `â„± n`-measurable, `limsup s atTop` is almost
everywhere equal to the set for which `âˆ‘ k, â„™(s (k + 1) | â„± k) = âˆ`. -/
theorem ae_mem_limsup_atTop_iff (Î¼ : Measure Î©) [IsFiniteMeasure Î¼] {s : â„• â†’ Set Î©}
    (hs : âˆ€ n, MeasurableSet[â„± n] (s n)) : âˆ€áµ Ï‰ âˆ‚Î¼, Ï‰ âˆˆ limsup s atTop â†”
    Tendsto (fun n => âˆ‘ k âˆˆ Finset.range n,
      (Î¼[(s (k + 1)).indicator (1 : Î© â†’ â„)|â„± k]) Ï‰) atTop atTop := by
  rw [â† limsup_nat_add s 1,
    Set.limsup_eq_tendsto_sum_indicator_atTop (zero_lt_one (Î± := â„)) (fun n â†¦ s (n + 1))]
  exact tendsto_sum_indicator_atTop_iff' hs

end MeasureTheory
