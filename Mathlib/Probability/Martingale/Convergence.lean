/-
Copyright (c) 2022 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import Mathlib.MeasureTheory.Constructions.Polish.Basic
import Mathlib.MeasureTheory.Function.UniformIntegrable
import Mathlib.Probability.Martingale.Upcrossing

/-!

# Martingale convergence theorems

The martingale convergence theorems are a collection of theorems characterizing the convergence
of a martingale provided it satisfies some boundedness conditions. This file contains the
almost everywhere martingale convergence theorem which provides an almost everywhere limit to
an LÂ¹ bounded submartingale. It also contains the LÂ¹ martingale convergence theorem which provides
an LÂ¹ limit to a uniformly integrable submartingale. Finally, it also contains the LÃ©vy upwards
theorems.

## Main results

* `MeasureTheory.Submartingale.ae_tendsto_limitProcess`: the almost everywhere martingale
  convergence theorem: an LÂ¹-bounded submartingale adapted to the filtration `â„±` converges almost
  everywhere to its limit process.
* `MeasureTheory.Submartingale.memLp_limitProcess`: the limit process of an Láµ–-bounded
  submartingale is Láµ–.
* `MeasureTheory.Submartingale.tendsto_eLpNorm_one_limitProcess`: part a of the LÂ¹ martingale
  convergence theorem: a uniformly integrable submartingale adapted to the filtration `â„±` converges
  almost everywhere and in LÂ¹ to an integrable function which is measurable with respect to
  the Ïƒ-algebra `â¨† n, â„± n`.
* `MeasureTheory.Martingale.ae_eq_condExp_limitProcess`: part b the LÂ¹ martingale convergence
  theorem: if `f` is a uniformly integrable martingale adapted to the filtration `â„±`, then
  `f n` equals `ğ”¼[g | â„± n]` almost everywhere where `g` is the limiting process of `f`.
* `MeasureTheory.Integrable.tendsto_ae_condExp`: part c the LÂ¹ martingale convergence theorem:
  given a `â¨† n, â„± n`-measurable function `g` where `â„±` is a filtration, `ğ”¼[g | â„± n]` converges
  almost everywhere to `g`.
* `MeasureTheory.Integrable.tendsto_eLpNorm_condExp`: part c the LÂ¹ martingale convergence theorem:
  given a `â¨† n, â„± n`-measurable function `g` where `â„±` is a filtration, `ğ”¼[g | â„± n]` converges in
  LÂ¹ to `g`.

-/


open TopologicalSpace Filter MeasureTheory.Filtration

open scoped NNReal ENNReal MeasureTheory ProbabilityTheory Topology

namespace MeasureTheory

variable {Î© : Type*} {m0 : MeasurableSpace Î©} {Î¼ : Measure Î©} {â„± : Filtration â„• m0}
variable {a b : â„} {f : â„• â†’ Î© â†’ â„} {Ï‰ : Î©} {R : â„â‰¥0}

section AeConvergence

/-!

### Almost everywhere martingale convergence theorem

We will now prove the almost everywhere martingale convergence theorem.

The a.e. martingale convergence theorem states: if `f` is an LÂ¹-bounded `â„±`-submartingale, then
it converges almost everywhere to an integrable function which is measurable with respect to
the Ïƒ-algebra `â„±âˆ := â¨† n, â„± n`.

Mathematically, we proceed by first noting that a real sequence $(x_n)$ converges if
(a) $\limsup_{n \to \infty} |x_n| < \infty$, (b) for all $a < b \in \mathbb{Q}$ we have the
number of upcrossings of $(x_n)$ from below $a$ to above $b$ is finite.
Thus, for all $\omega$ satisfying $\limsup_{n \to \infty} |f_n(\omega)| < \infty$ and the number of
upcrossings of $(f_n(\omega))$ from below $a$ to above $b$ is finite for all $a < b \in \mathbb{Q}$,
we have $(f_n(\omega))$ is convergent.

Hence, assuming $(f_n)$ is LÂ¹-bounded, using Fatou's lemma, we have
$$
  \mathbb{E} \limsup_{n \to \infty} |f_n| \le \limsup_{n \to \infty} \mathbb{E}|f_n| < \infty
$$
implying $\limsup_{n \to \infty} |f_n| < \infty$ a.e. Furthermore, by the upcrossing estimate,
the number of upcrossings is finite almost everywhere implying $f$ converges pointwise almost
everywhere.

Thus, denoting $g$ the a.e. limit of $(f_n)$, $g$ is $\mathcal{F}_\infty$-measurable as for all
$n$, $f_n$ is $\mathcal{F}_n$-measurable and $\mathcal{F}_n \le \mathcal{F}_\infty$. Finally, $g$
is integrable as $|g| \le \liminf_{n \to \infty} |f_n|$ so
$$
  \mathbb{E}|g| \le \mathbb{E} \limsup_{n \to \infty} |f_n| \le
    \limsup_{n \to \infty} \mathbb{E}|f_n| < \infty
$$
as required.

Implementationwise, we have `tendsto_of_no_upcrossings` which shows that
a bounded sequence converges if it does not visit below $a$ and above $b$ infinitely often
for all $a, b âˆˆ s$ for some dense set $s$. So, we may skip the first step provided we can prove
that the realizations are bounded almost everywhere. Indeed, suppose $|f_n(\omega)|$ is not
bounded, then either $f_n(\omega) \to \pm \infty$ or one of $\limsup f_n(\omega)$ or
$\liminf f_n(\omega)$ equals $\pm \infty$ while the other is finite. But the first case
contradicts $\liminf |f_n(\omega)| < \infty$ while the second case contradicts finite upcrossings.

Furthermore, we introduce `Filtration.limitProcess` which chooses the limiting random variable
of a stochastic process if it exists, otherwise returning 0. Hence, instead of showing an
existence statement, we phrase the a.e. martingale convergence theorem by showing that a
submartingale converges to its `limitProcess` almost everywhere.

-/


/-- If a stochastic process has bounded upcrossing from below `a` to above `b`,
then it does not frequently visit both below `a` and above `b`. -/
theorem not_frequently_of_upcrossings_lt_top (hab : a < b) (hÏ‰ : upcrossings a b f Ï‰ â‰  âˆ) :
    Â¬((âˆƒá¶  n in atTop, f n Ï‰ < a) âˆ§ âˆƒá¶  n in atTop, b < f n Ï‰) := by
  rw [â† lt_top_iff_ne_top, upcrossings_lt_top_iff] at hÏ‰
  replace hÏ‰ : âˆƒ k, âˆ€ N, upcrossingsBefore a b f N Ï‰ < k := by
    obtain âŸ¨k, hkâŸ© := hÏ‰
    exact âŸ¨k + 1, fun N => lt_of_le_of_lt (hk N) k.lt_succ_selfâŸ©
  rintro âŸ¨hâ‚, hâ‚‚âŸ©
  rw [frequently_atTop] at hâ‚ hâ‚‚
  refine Classical.not_not.2 hÏ‰ ?_
  push_neg
  intro k
  induction k with
  | zero => simp only [zero_le, exists_const]
  | succ k ih =>
    obtain âŸ¨N, hNâŸ© := ih
    obtain âŸ¨Nâ‚, hNâ‚, hNâ‚'âŸ© := hâ‚ N
    obtain âŸ¨Nâ‚‚, hNâ‚‚, hNâ‚‚'âŸ© := hâ‚‚ Nâ‚
    exact âŸ¨Nâ‚‚ + 1, Nat.succ_le_of_lt <|
      lt_of_le_of_lt hN (upcrossingsBefore_lt_of_exists_upcrossing hab hNâ‚ hNâ‚' hNâ‚‚ hNâ‚‚')âŸ©

/-- A stochastic process that frequently visits below `a` and above `b` has infinite upcrossings. -/
theorem upcrossings_eq_top_of_frequently_lt (hab : a < b) (hâ‚ : âˆƒá¶  n in atTop, f n Ï‰ < a)
    (hâ‚‚ : âˆƒá¶  n in atTop, b < f n Ï‰) : upcrossings a b f Ï‰ = âˆ :=
  by_contradiction fun h => not_frequently_of_upcrossings_lt_top hab h âŸ¨hâ‚, hâ‚‚âŸ©

/-- A realization of a stochastic process with bounded upcrossings and bounded liminfs is
convergent.

We use the spelling `< âˆ` instead of the standard `â‰  âˆ` in the assumptions since it is not as easy
to change `<` to `â‰ ` under binders. -/
theorem tendsto_of_uncrossing_lt_top (hfâ‚ : liminf (fun n => (â€–f n Ï‰â€–â‚Š : â„â‰¥0âˆ)) atTop < âˆ)
    (hfâ‚‚ : âˆ€ a b : â„š, a < b â†’ upcrossings a b f Ï‰ < âˆ) :
    âˆƒ c, Tendsto (fun n => f n Ï‰) atTop (ğ“ c) := by
  by_cases h : IsBoundedUnder (Â· â‰¤ Â·) atTop fun n => |f n Ï‰|
  Â· rw [isBoundedUnder_le_abs] at h
    refine tendsto_of_no_upcrossings Rat.denseRange_cast ?_ h.1 h.2
    rintro _ âŸ¨a, rflâŸ© _ âŸ¨b, rflâŸ© hab
    exact not_frequently_of_upcrossings_lt_top hab (hfâ‚‚ a b (Rat.cast_lt.1 hab)).ne
  Â· obtain âŸ¨a, b, hab, hâ‚, hâ‚‚âŸ© := ENNReal.exists_upcrossings_of_not_bounded_under hfâ‚.ne h
    exact
      False.elim ((hfâ‚‚ a b hab).ne (upcrossings_eq_top_of_frequently_lt (Rat.cast_lt.2 hab) hâ‚ hâ‚‚))

/-- An LÂ¹-bounded submartingale has bounded upcrossings almost everywhere. -/
theorem Submartingale.upcrossings_ae_lt_top' [IsFiniteMeasure Î¼] (hf : Submartingale f â„± Î¼)
    (hbdd : âˆ€ n, eLpNorm (f n) 1 Î¼ â‰¤ R) (hab : a < b) : âˆ€áµ Ï‰ âˆ‚Î¼, upcrossings a b f Ï‰ < âˆ := by
  refine ae_lt_top (hf.adapted.measurable_upcrossings hab) ?_
  have := hf.mul_lintegral_upcrossings_le_lintegral_pos_part a b
  rw [mul_comm, â† ENNReal.le_div_iff_mul_le] at this
  Â· refine (lt_of_le_of_lt this (ENNReal.div_lt_top ?_ ?_)).ne
    Â· have hR' : âˆ€ n, âˆ«â» Ï‰, â€–f n Ï‰ - aâ€–â‚Š âˆ‚Î¼ â‰¤ R + â€–aâ€–â‚Š * Î¼ Set.univ := by
        simp_rw [eLpNorm_one_eq_lintegral_enorm] at hbdd
        intro n
        refine (lintegral_mono ?_ : âˆ«â» Ï‰, â€–f n Ï‰ - aâ€–â‚Š âˆ‚Î¼ â‰¤ âˆ«â» Ï‰, â€–f n Ï‰â€–â‚Š + â€–aâ€–â‚Š âˆ‚Î¼).trans ?_
        Â· intro Ï‰
          simp_rw [sub_eq_add_neg, â† nnnorm_neg a, â† ENNReal.coe_add, ENNReal.coe_le_coe]
          exact nnnorm_add_le _ _
        Â· simp_rw [lintegral_add_right _ measurable_const, lintegral_const]
          exact add_le_add (hbdd _) le_rfl
      refine ne_of_lt (iSup_lt_iff.2 âŸ¨R + â€–aâ€–â‚Š * Î¼ Set.univ, ENNReal.add_lt_top.2
        âŸ¨ENNReal.coe_lt_top, by finitenessâŸ©,
        fun n => le_trans ?_ (hR' n)âŸ©)
      refine lintegral_mono fun Ï‰ => ?_
      rw [ENNReal.ofReal_le_iff_le_toReal, ENNReal.coe_toReal, coe_nnnorm]
      Â· by_cases hnonneg : 0 â‰¤ f n Ï‰ - a
        Â· rw [posPart_eq_self.2 hnonneg, Real.norm_eq_abs, abs_of_nonneg hnonneg]
        Â· rw [posPart_eq_zero.2 (not_le.1 hnonneg).le]
          exact norm_nonneg _
      Â· finiteness
    Â· simp only [hab, Ne, ENNReal.ofReal_eq_zero, sub_nonpos, not_le]
  Â· left; simp only [hab, Ne, ENNReal.ofReal_eq_zero, sub_nonpos, not_le]
  Â· left; finiteness

theorem Submartingale.upcrossings_ae_lt_top [IsFiniteMeasure Î¼] (hf : Submartingale f â„± Î¼)
    (hbdd : âˆ€ n, eLpNorm (f n) 1 Î¼ â‰¤ R) : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ a b : â„š, a < b â†’ upcrossings a b f Ï‰ < âˆ := by
  simp only [ae_all_iff, eventually_imp_distrib_left]
  rintro a b hab
  exact hf.upcrossings_ae_lt_top' hbdd (Rat.cast_lt.2 hab)

/-- An LÂ¹-bounded submartingale converges almost everywhere. -/
theorem Submartingale.exists_ae_tendsto_of_bdd [IsFiniteMeasure Î¼] (hf : Submartingale f â„± Î¼)
    (hbdd : âˆ€ n, eLpNorm (f n) 1 Î¼ â‰¤ R) : âˆ€áµ Ï‰ âˆ‚Î¼, âˆƒ c, Tendsto (fun n => f n Ï‰) atTop (ğ“ c) := by
  filter_upwards [hf.upcrossings_ae_lt_top hbdd, ae_bdd_liminf_atTop_of_eLpNorm_bdd one_ne_zero
    (fun n => (hf.stronglyMeasurable n).measurable.mono (â„±.le n) le_rfl) hbdd] with Ï‰ hâ‚ hâ‚‚
  exact tendsto_of_uncrossing_lt_top hâ‚‚ hâ‚

theorem Submartingale.exists_ae_trim_tendsto_of_bdd [IsFiniteMeasure Î¼] (hf : Submartingale f â„± Î¼)
    (hbdd : âˆ€ n, eLpNorm (f n) 1 Î¼ â‰¤ R) :
    âˆ€áµ Ï‰ âˆ‚Î¼.trim (sSup_le fun _ âŸ¨_, hnâŸ© => hn â–¸ â„±.le _ : â¨† n, â„± n â‰¤ m0),
      âˆƒ c, Tendsto (fun n => f n Ï‰) atTop (ğ“ c) := by
  letI := (â¨† n, â„± n)
  rw [ae_iff, trim_measurableSet_eq]
  Â· exact hf.exists_ae_tendsto_of_bdd hbdd
  Â· exact MeasurableSet.compl <| measurableSet_exists_tendsto
      fun n => (hf.stronglyMeasurable n).measurable.mono (le_sSup âŸ¨n, rflâŸ©) le_rfl

/-- **Almost everywhere martingale convergence theorem**: An LÂ¹-bounded submartingale converges
almost everywhere to a `â¨† n, â„± n`-measurable function. -/
theorem Submartingale.ae_tendsto_limitProcess [IsFiniteMeasure Î¼] (hf : Submartingale f â„± Î¼)
    (hbdd : âˆ€ n, eLpNorm (f n) 1 Î¼ â‰¤ R) :
    âˆ€áµ Ï‰ âˆ‚Î¼, Tendsto (fun n => f n Ï‰) atTop (ğ“ (â„±.limitProcess f Î¼ Ï‰)) := by
  classical
  suffices
      âˆƒ g, StronglyMeasurable[â¨† n, â„± n] g âˆ§ âˆ€áµ Ï‰ âˆ‚Î¼, Tendsto (fun n => f n Ï‰) atTop (ğ“ (g Ï‰)) by
    rw [limitProcess, dif_pos this]
    exact (Classical.choose_spec this).2
  set g' : Î© â†’ â„ := fun Ï‰ => if h : âˆƒ c, Tendsto (fun n => f n Ï‰) atTop (ğ“ c) then h.choose else 0
  have hle : â¨† n, â„± n â‰¤ m0 := sSup_le fun m âŸ¨n, hnâŸ© => hn â–¸ â„±.le _
  have hg' : âˆ€áµ Ï‰ âˆ‚Î¼.trim hle, Tendsto (fun n => f n Ï‰) atTop (ğ“ (g' Ï‰)) := by
    filter_upwards [hf.exists_ae_trim_tendsto_of_bdd hbdd] with Ï‰ hÏ‰
    simp_rw [g', dif_pos hÏ‰]
    exact hÏ‰.choose_spec
  have hg'm : AEStronglyMeasurable[â¨† n, â„± n] g' (Î¼.trim hle) :=
    (@aemeasurable_of_tendsto_metrizable_ae' _ _ (â¨† n, â„± n) _ _ _ _ _ _ _
      (fun n => ((hf.stronglyMeasurable n).measurable.mono (le_sSup âŸ¨n, rflâŸ© : â„± n â‰¤ â¨† n, â„± n)
        le_rfl).aemeasurable) hg').aestronglyMeasurable
  obtain âŸ¨g, hgm, haeâŸ© := hg'm
  have hg : âˆ€áµ Ï‰ âˆ‚Î¼.trim hle, Tendsto (fun n => f n Ï‰) atTop (ğ“ (g Ï‰)) := by
    filter_upwards [hae, hg'] with Ï‰ hÏ‰ hg'Ï‰
    exact hÏ‰ â–¸ hg'Ï‰
  exact âŸ¨g, hgm, measure_eq_zero_of_trim_eq_zero hle hgâŸ©

/-- The limiting process of an Láµ–-bounded submartingale is Láµ–. -/
theorem Submartingale.memLp_limitProcess {p : â„â‰¥0âˆ} (hf : Submartingale f â„± Î¼)
    (hbdd : âˆ€ n, eLpNorm (f n) p Î¼ â‰¤ R) : MemLp (â„±.limitProcess f Î¼) p Î¼ :=
  memLp_limitProcess_of_eLpNorm_bdd
    (fun n => ((hf.stronglyMeasurable n).mono (â„±.le n)).aestronglyMeasurable) hbdd

@[deprecated (since := "2025-02-21")]
alias Submartingale.memâ„’p_limitProcess := Submartingale.memLp_limitProcess

end AeConvergence

section L1Convergence

variable [IsFiniteMeasure Î¼] {g : Î© â†’ â„}

/-!

### LÂ¹ martingale convergence theorem

We will now prove the LÂ¹ martingale convergence theorems.

The LÂ¹ martingale convergence theorem states that:
(a) if `f` is a uniformly integrable (in the probability sense) submartingale adapted to the
  filtration `â„±`, it converges in LÂ¹ to an integrable function `g` which is measurable with
  respect to `â„±âˆ := â¨† n, â„± n` and
(b) if `f` is actually a martingale, `f n = ğ”¼[g | â„± n]` almost everywhere.
(c) Finally, if `h` is integrable and measurable with respect to `â„±âˆ`, `(ğ”¼[h | â„± n])â‚™` is a
  uniformly integrable martingale which converges to `h` almost everywhere and in LÂ¹.

The proof is quite simple. (a) follows directly from the a.e. martingale convergence theorem
and the Vitali convergence theorem as our definition of uniform integrability (in the probability
sense) directly implies LÂ¹-uniform boundedness. We note that our definition of uniform
integrability is slightly non-standard but is equivalent to the usual literary definition. This
equivalence is provided by `MeasureTheory.uniformIntegrable_iff`.

(b) follows since given $n$, we have for all $m \ge n$,
$$
  \|f_n - \mathbb{E}[g \mid \mathcal{F}_n]\|_1 =
    \|\mathbb{E}[f_m - g \mid \mathcal{F}_n]\|_1 \le \|\|f_m - g\|_1.
$$
Thus, taking $m \to \infty$ provides the almost everywhere equality.

Finally, to prove (c), we define $f_n := \mathbb{E}[h \mid \mathcal{F}_n]$. It is clear that
$(f_n)_n$ is a martingale by the tower property for conditional expectations. Furthermore,
$(f_n)_n$ is uniformly integrable in the probability sense. Indeed, as a single function is
uniformly integrable in the measure theory sense, for all $\epsilon > 0$, there exists some
$\delta > 0$ such that for all measurable set $A$ with $\mu(A) < Î´$, we have
$\mathbb{E}|h|\mathbf{1}_A < \epsilon$. So, since for sufficiently large $\lambda$, by the Markov
inequality, we have for all $n$,
$$
  \mu(|f_n| \ge \lambda) \le \lambda^{-1}\mathbb{E}|f_n| \le \lambda^{-1}\mathbb|g| < \delta,
$$
we have for sufficiently large $\lambda$, for all $n$,
$$
  \mathbb{E}|f_n|\mathbf{1}_{|f_n| \ge \lambda} \le
    \mathbb|g|\mathbf{1}_{|f_n| \ge \lambda} < \epsilon,
$$
implying $(f_n)_n$ is uniformly integrable. Now, to prove $f_n \to h$ almost everywhere and in
LÂ¹, it suffices to show that $h = g$ almost everywhere where $g$ is the almost everywhere and LÂ¹
limit of $(f_n)_n$ from part (b) of the theorem. By noting that, for all $s \in \mathcal{F}_n$,
we have
$$
  \mathbb{E}g\mathbf{1}_s = \mathbb{E}[\mathbb{E}[g \mid \mathcal{F}_n]\mathbf{1}_s] =
    \mathbb{E}[\mathbb{E}[h \mid \mathcal{F}_n]\mathbf{1}_s] = \mathbb{E}h\mathbf{1}_s
$$
where $\mathbb{E}[g \mid \mathcal{F}_n = \mathbb{E}[h \mid \mathcal{F}_n]$ almost everywhere
by part (b); the equality also holds for all $s \in \mathcal{F}_\infty$ by Dynkin's theorem.
Thus, as both $h$ and $g$ are $\mathcal{F}_\infty$-measurable, $h = g$ almost everywhere as
required.

Similar to the a.e. martingale convergence theorem, rather than showing the existence of the
limiting process, we phrase the LÂ¹-martingale convergence theorem by proving that a submartingale
does converge in LÂ¹ to its `limitProcess`. However, in contrast to the a.e. martingale convergence
theorem, we do not need to introduce an LÂ¹ version of `Filtration.limitProcess` as the LÂ¹ limit
and the a.e. limit of a submartingale coincide.

-/


/-- Part a of the **LÂ¹ martingale convergence theorem**: a uniformly integrable submartingale
adapted to the filtration `â„±` converges a.e. and in LÂ¹ to an integrable function which is
measurable with respect to the Ïƒ-algebra `â¨† n, â„± n`. -/
theorem Submartingale.tendsto_eLpNorm_one_limitProcess (hf : Submartingale f â„± Î¼)
    (hunif : UniformIntegrable f 1 Î¼) :
    Tendsto (fun n => eLpNorm (f n - â„±.limitProcess f Î¼) 1 Î¼) atTop (ğ“ 0) := by
  obtain âŸ¨R, hRâŸ© := hunif.2.2
  have hmeas : âˆ€ n, AEStronglyMeasurable (f n) Î¼ := fun n =>
    ((hf.stronglyMeasurable n).mono (â„±.le _)).aestronglyMeasurable
  exact tendsto_Lp_finite_of_tendstoInMeasure le_rfl ENNReal.one_ne_top hmeas
    (memLp_limitProcess_of_eLpNorm_bdd hmeas hR) hunif.2.1
    (tendstoInMeasure_of_tendsto_ae hmeas <| hf.ae_tendsto_limitProcess hR)

theorem Submartingale.ae_tendsto_limitProcess_of_uniformIntegrable (hf : Submartingale f â„± Î¼)
    (hunif : UniformIntegrable f 1 Î¼) :
    âˆ€áµ Ï‰ âˆ‚Î¼, Tendsto (fun n => f n Ï‰) atTop (ğ“ (â„±.limitProcess f Î¼ Ï‰)) :=
  let âŸ¨_, hRâŸ© := hunif.2.2
  hf.ae_tendsto_limitProcess hR

/-- If a martingale `f` adapted to `â„±` converges in LÂ¹ to `g`, then for all `n`, `f n` is almost
everywhere equal to `ğ”¼[g | â„± n]`. -/
theorem Martingale.eq_condExp_of_tendsto_eLpNorm {Î¼ : Measure Î©} (hf : Martingale f â„± Î¼)
    (hg : Integrable g Î¼) (hgtends : Tendsto (fun n => eLpNorm (f n - g) 1 Î¼) atTop (ğ“ 0)) (n : â„•) :
    f n =áµ[Î¼] Î¼[g|â„± n] := by
  rw [â† sub_ae_eq_zero, â† eLpNorm_eq_zero_iff (((hf.stronglyMeasurable n).mono (â„±.le _)).sub
    (stronglyMeasurable_condExp.mono (â„±.le _))).aestronglyMeasurable one_ne_zero]
  have ht : Tendsto (fun m => eLpNorm (Î¼[f m - g|â„± n]) 1 Î¼) atTop (ğ“ 0) :=
    haveI hint : âˆ€ m, Integrable (f m - g) Î¼ := fun m => (hf.integrable m).sub hg
    tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds hgtends (fun m => zero_le _)
      fun m => eLpNorm_one_condExp_le_eLpNorm _
  have hev : âˆ€ m â‰¥ n, eLpNorm (Î¼[f m - g|â„± n]) 1 Î¼ = eLpNorm (f n - Î¼[g|â„± n]) 1 Î¼ := by
    refine fun m hm => eLpNorm_congr_ae ((condExp_sub (hf.integrable m) hg _).trans ?_)
    filter_upwards [hf.2 n m hm] with x hx
    simp only [hx, Pi.sub_apply]
  exact tendsto_nhds_unique (tendsto_atTop_of_eventually_const hev) ht

/-- Part b of the **LÂ¹ martingale convergence theorem**: if `f` is a uniformly integrable martingale
adapted to the filtration `â„±`, then for all `n`, `f n` is almost everywhere equal to the conditional
expectation of its limiting process wrt. `â„± n`. -/
theorem Martingale.ae_eq_condExp_limitProcess (hf : Martingale f â„± Î¼)
    (hbdd : UniformIntegrable f 1 Î¼) (n : â„•) : f n =áµ[Î¼] Î¼[â„±.limitProcess f Î¼|â„± n] :=
  let âŸ¨_, hRâŸ© := hbdd.2.2
  hf.eq_condExp_of_tendsto_eLpNorm ((memLp_limitProcess_of_eLpNorm_bdd hbdd.1 hR).integrable le_rfl)
    (hf.submartingale.tendsto_eLpNorm_one_limitProcess hbdd) n

/-- Part c of the **LÂ¹ martingale convergence theorem**: Given an integrable function `g` which
is measurable with respect to `â¨† n, â„± n` where `â„±` is a filtration, the martingale defined by
`ğ”¼[g | â„± n]` converges almost everywhere to `g`.

This martingale also converges to `g` in LÂ¹ and this result is provided by
`MeasureTheory.Integrable.tendsto_eLpNorm_condExp` -/
theorem Integrable.tendsto_ae_condExp (hg : Integrable g Î¼)
    (hgmeas : StronglyMeasurable[â¨† n, â„± n] g) :
    âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => (Î¼[g|â„± n]) x) atTop (ğ“ (g x)) := by
  have hle : â¨† n, â„± n â‰¤ m0 := sSup_le fun m âŸ¨n, hnâŸ© => hn â–¸ â„±.le _
  have hunif : UniformIntegrable (fun n => Î¼[g|â„± n]) 1 Î¼ :=
    hg.uniformIntegrable_condExp_filtration
  obtain âŸ¨R, hRâŸ© := hunif.2.2
  have hlimint : Integrable (â„±.limitProcess (fun n => Î¼[g|â„± n]) Î¼) Î¼ :=
    (memLp_limitProcess_of_eLpNorm_bdd hunif.1 hR).integrable le_rfl
  suffices g =áµ[Î¼] â„±.limitProcess (fun n x => (Î¼[g|â„± n]) x) Î¼ by
    filter_upwards [this, (martingale_condExp g â„± Î¼).submartingale.ae_tendsto_limitProcess hR] with
      x heq ht
    rwa [heq]
  have : âˆ€ n s, MeasurableSet[â„± n] s â†’
      âˆ« x in s, g x âˆ‚Î¼ = âˆ« x in s, â„±.limitProcess (fun n x => (Î¼[g|â„± n]) x) Î¼ x âˆ‚Î¼ := by
    intro n s hs
    rw [â† setIntegral_condExp (â„±.le n) hg hs, â† setIntegral_condExp (â„±.le n) hlimint hs]
    refine setIntegral_congr_ae (â„±.le _ _ hs) ?_
    filter_upwards [(martingale_condExp g â„± Î¼).ae_eq_condExp_limitProcess hunif n] with x hx _
    rw [hx]
  refine ae_eq_of_forall_setIntegral_eq_of_sigmaFinite' hle (fun s _ _ => hg.integrableOn)
    (fun s _ _ => hlimint.integrableOn) (fun s hs _ => ?_) hgmeas.aestronglyMeasurable
    stronglyMeasurable_limitProcess.aestronglyMeasurable
  have hpi : IsPiSystem {s | âˆƒ n, MeasurableSet[â„± n] s} := by
    rw [Set.setOf_exists]
    exact isPiSystem_iUnion_of_monotone _ (fun n â†¦ (â„± n).isPiSystem_measurableSet) fun _ _ â†¦ â„±.mono
  induction s, hs
    using MeasurableSpace.induction_on_inter (MeasurableSpace.measurableSpace_iSup_eq â„±) hpi with
  | empty =>
    simp only [Measure.restrict_empty, integral_zero_measure]
  | basic s hs =>
    rcases hs with âŸ¨n, hnâŸ©
    exact this n _ hn
  | compl t htmeas ht =>
    have hgeq := @setIntegral_compl _ _ (â¨† n, â„± n) _ _ _ _ _ htmeas (hg.trim hle hgmeas)
    have hheq := @setIntegral_compl _ _ (â¨† n, â„± n) _ _ _ _ _ htmeas
      (hlimint.trim hle stronglyMeasurable_limitProcess)
    rw [setIntegral_trim hle hgmeas htmeas.compl,
      setIntegral_trim hle stronglyMeasurable_limitProcess htmeas.compl, hgeq, hheq, â†
      setIntegral_trim hle hgmeas htmeas, â†
      setIntegral_trim hle stronglyMeasurable_limitProcess htmeas, â† integral_trim hle hgmeas, â†
      integral_trim hle stronglyMeasurable_limitProcess, â† setIntegral_univ,
      this 0 _ MeasurableSet.univ, setIntegral_univ, ht (measure_lt_top _ _)]
  | iUnion f hf hfmeas heq =>
    rw [integral_iUnion (fun n => hle _ (hfmeas n)) hf hg.integrableOn,
      integral_iUnion (fun n => hle _ (hfmeas n)) hf hlimint.integrableOn]
    exact tsum_congr fun n => heq _ (measure_lt_top _ _)

/-- Part c of the **LÂ¹ martingale convergence theorem**: Given an integrable function `g` which
is measurable with respect to `â¨† n, â„± n` where `â„±` is a filtration, the martingale defined by
`ğ”¼[g | â„± n]` converges in LÂ¹ to `g`.

This martingale also converges to `g` almost everywhere and this result is provided by
`MeasureTheory.Integrable.tendsto_ae_condExp` -/
theorem Integrable.tendsto_eLpNorm_condExp (hg : Integrable g Î¼)
    (hgmeas : StronglyMeasurable[â¨† n, â„± n] g) :
    Tendsto (fun n => eLpNorm (Î¼[g|â„± n] - g) 1 Î¼) atTop (ğ“ 0) :=
  tendsto_Lp_finite_of_tendstoInMeasure le_rfl ENNReal.one_ne_top
    (fun n => (stronglyMeasurable_condExp.mono (â„±.le n)).aestronglyMeasurable)
    (memLp_one_iff_integrable.2 hg) hg.uniformIntegrable_condExp_filtration.2.1
    (tendstoInMeasure_of_tendsto_ae
      (fun n => (stronglyMeasurable_condExp.mono (â„±.le n)).aestronglyMeasurable)
      (hg.tendsto_ae_condExp hgmeas))

/-- **LÃ©vy's upward theorem**, almost everywhere version: given a function `g` and a filtration
`â„±`, the sequence defined by `ğ”¼[g | â„± n]` converges almost everywhere to `ğ”¼[g | â¨† n, â„± n]`. -/
theorem tendsto_ae_condExp (g : Î© â†’ â„) :
    âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => (Î¼[g|â„± n]) x) atTop (ğ“ ((Î¼[g|â¨† n, â„± n]) x)) := by
  have ht : âˆ€áµ x âˆ‚Î¼, Tendsto (fun n => (Î¼[Î¼[g|â¨† n, â„± n]|â„± n]) x) atTop (ğ“ ((Î¼[g|â¨† n, â„± n]) x)) :=
    integrable_condExp.tendsto_ae_condExp stronglyMeasurable_condExp
  have heq : âˆ€ n, âˆ€áµ x âˆ‚Î¼, (Î¼[Î¼[g|â¨† n, â„± n]|â„± n]) x = (Î¼[g|â„± n]) x := fun n =>
    condExp_condExp_of_le (le_iSup _ n) (iSup_le fun n => â„±.le n)
  rw [â† ae_all_iff] at heq
  filter_upwards [heq, ht] with x hxeq hxt
  exact hxt.congr hxeq

@[deprecated (since := "2025-01-21")] alias tendsto_ae_condexp := tendsto_ae_condExp

/-- **LÃ©vy's upward theorem**, LÂ¹ version: given a function `g` and a filtration `â„±`, the
sequence defined by `ğ”¼[g | â„± n]` converges in LÂ¹ to `ğ”¼[g | â¨† n, â„± n]`. -/
theorem tendsto_eLpNorm_condExp (g : Î© â†’ â„) :
    Tendsto (fun n => eLpNorm (Î¼[g|â„± n] - Î¼[g|â¨† n, â„± n]) 1 Î¼) atTop (ğ“ 0) := by
  have ht : Tendsto (fun n => eLpNorm (Î¼[Î¼[g|â¨† n, â„± n]|â„± n] - Î¼[g|â¨† n, â„± n]) 1 Î¼) atTop (ğ“ 0) :=
    integrable_condExp.tendsto_eLpNorm_condExp stronglyMeasurable_condExp
  have heq : âˆ€ n, âˆ€áµ x âˆ‚Î¼, (Î¼[Î¼[g|â¨† n, â„± n]|â„± n]) x = (Î¼[g|â„± n]) x := fun n =>
    condExp_condExp_of_le (le_iSup _ n) (iSup_le fun n => â„±.le n)
  refine ht.congr fun n => eLpNorm_congr_ae ?_
  filter_upwards [heq n] with x hxeq
  simp only [hxeq, Pi.sub_apply]

@[deprecated (since := "2025-01-21")] alias tendsto_eLpNorm_condexp := tendsto_eLpNorm_condExp

end L1Convergence

end MeasureTheory
