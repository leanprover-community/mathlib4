/-
Copyright (c) 2022 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import Mathlib.Probability.Martingale.Upcrossing
import Mathlib.MeasureTheory.Function.UniformIntegrable
import Mathlib.MeasureTheory.Constructions.Polish

#align_import probability.martingale.convergence from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!

# Martingale convergence theorems

The martingale convergence theorems are a collection of theorems characterizing the convergence
of a martingale provided it satisfies some boundedness conditions. This file contains the
almost everywhere martingale convergence theorem which provides an almost everywhere limit to
an L¬π bounded submartingale. It also contains the L¬π martingale convergence theorem which provides
an L¬π limit to a uniformly integrable submartingale. Finally, it also contains the L√©vy upwards
theorems.

## Main results

* `MeasureTheory.Submartingale.ae_tendsto_limitProcess`: the almost everywhere martingale
  convergence theorem: an L¬π-bounded submartingale adapted to the filtration `‚Ñ±` converges almost
  everywhere to its limit process.
* `MeasureTheory.Submartingale.mem‚Ñíp_limitProcess`: the limit process of an L·µñ-bounded
  submartingale is L·µñ.
* `MeasureTheory.Submartingale.tendsto_snorm_one_limitProcess`: part a of the L¬π martingale
  convergence theorem: a uniformly integrable submartingale adapted to the filtration `‚Ñ±` converges
  almost everywhere and in L¬π to an integrable function which is measurable with respect to
  the œÉ-algebra `‚®Ü n, ‚Ñ± n`.
* `MeasureTheory.Martingale.ae_eq_condexp_limitProcess`: part b the L¬π martingale convergence
  theorem: if `f` is a uniformly integrable martingale adapted to the filtration `‚Ñ±`, then
  `f n` equals `ùîº[g | ‚Ñ± n]` almost everywhere where `g` is the limiting process of `f`.
* `MeasureTheory.Integrable.tendsto_ae_condexp`: part c the L¬π martingale convergence theorem:
  given a `‚®Ü n, ‚Ñ± n`-measurable function `g` where `‚Ñ±` is a filtration, `ùîº[g | ‚Ñ± n]` converges
  almost everywhere to `g`.
* `MeasureTheory.Integrable.tendsto_snorm_condexp`: part c the L¬π martingale convergence theorem:
  given a `‚®Ü n, ‚Ñ± n`-measurable function `g` where `‚Ñ±` is a filtration, `ùîº[g | ‚Ñ± n]` converges in
  L¬π to `g`.

-/


open TopologicalSpace Filter MeasureTheory.Filtration

open scoped NNReal ENNReal MeasureTheory ProbabilityTheory BigOperators Topology

namespace MeasureTheory

variable {Œ© Œπ : Type*} {m0 : MeasurableSpace Œ©} {Œº : Measure Œ©} {‚Ñ± : Filtration ‚Ñï m0}

variable {a b : ‚Ñù} {f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù} {œâ : Œ©} {R : ‚Ñù‚â•0}

section AeConvergence

/-!

### Almost everywhere martingale convergence theorem

We will now prove the almost everywhere martingale convergence theorem.

The a.e. martingale convergence theorem states: if `f` is an L¬π-bounded `‚Ñ±`-submartingale, then
it converges almost everywhere to an integrable function which is measurable with respect to
the œÉ-algebra `‚Ñ±‚àû := ‚®Ü n, ‚Ñ± n`.

Mathematically, we proceed by first noting that a real sequence $(x_n)$ converges if
(a) $\limsup_{n \to \infty} |x_n| < \infty$, (b) for all $a < b \in \mathbb{Q}$ we have the
number of upcrossings of $(x_n)$ from below $a$ to above $b$ is finite.
Thus, for all $\omega$ satisfying $\limsup_{n \to \infty} |f_n(\omega)| < \infty$ and the number of
upcrossings of $(f_n(\omega))$ from below $a$ to above $b$ is finite for all $a < b \in \mathbb{Q}$,
we have $(f_n(\omega))$ is convergent.

Hence, assuming $(f_n)$ is L¬π-bounded, using Fatou's lemma, we have
$$
  \mathbb{E} \limsup_{n \to \infty} |f_n| \le \limsup_{n \to \infty} \mathbb{E}|f_n| < \infty
$$
implying $\limsup_{n \to \infty} |f_n| < \infty$ a.e. Furthermore, by the upcrossing estimate,
the number of upcrossings is finite almost everywhere implying $f$ converges pointwise almost
everywhere.

Thus, denoting $g$ the a.e. limit of $(f_n)$, $g$ is $\mathcal{F}_\infty$-measurable as for all
$n$, $f_n$ is $\mathcal{F}_n$-measurable and $\mathcal{F}_n \le \mathcal{F}_\infty$. Finally, $g$
is integrable as $|g| \le \liminf_{n \to \infty} |f_n|$ so
$$
  \mathbb{E}|g| \le \mathbb{E} \limsup_{n \to \infty} |f_n| \le
    \limsup_{n \to \infty} \mathbb{E}|f_n| < \infty
$$
as required.

Implementationwise, we have `tendsto_of_no_upcrossings` which shows that
a bounded sequence converges if it does not visit below $a$ and above $b$ infinitely often
for all $a, b ‚àà s$ for some dense set $s$. So, we may skip the first step provided we can prove
that the realizations are bounded almost everywhere. Indeed, suppose $|f_n(\omega)|$ is not
bounded, then either $f_n(\omega) \to \pm \infty$ or one of $\limsup f_n(\omega)$ or
$\liminf f_n(\omega)$ equals $\pm \infty$ while the other is finite. But the first case
contradicts $\liminf |f_n(\omega)| < \infty$ while the second case contradicts finite upcrossings.

Furthermore, we introduce `Filtration.limitProcess` which chooses the limiting random variable
of a stochastic process if it exists, otherwise returning 0. Hence, instead of showing an
existence statement, we phrase the a.e. martingale convergence theorem by showing that a
submartingale converges to its `limitProcess` almost everywhere.

-/


/-- If a stochastic process has bounded upcrossing from below `a` to above `b`,
then it does not frequently visit both below `a` and above `b`. -/
theorem not_frequently_of_upcrossings_lt_top (hab : a < b) (hœâ : upcrossings a b f œâ ‚â† ‚àû) :
    ¬¨((‚àÉ·∂† n in atTop, f n œâ < a) ‚àß ‚àÉ·∂† n in atTop, b < f n œâ) := by
  rw [‚Üê lt_top_iff_ne_top, upcrossings_lt_top_iff] at hœâ
  replace hœâ : ‚àÉ k, ‚àÄ N, upcrossingsBefore a b f N œâ < k
  ¬∑ obtain ‚ü®k, hk‚ü© := hœâ
    exact ‚ü®k + 1, fun N => lt_of_le_of_lt (hk N) k.lt_succ_self‚ü©
  rintro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©
  rw [frequently_atTop] at h‚ÇÅ h‚ÇÇ
  refine' Classical.not_not.2 hœâ _
  push_neg
  intro k
  induction' k with k ih
  ¬∑ simp only [Nat.zero_eq, zero_le, exists_const]
  ¬∑ obtain ‚ü®N, hN‚ü© := ih
    obtain ‚ü®N‚ÇÅ, hN‚ÇÅ, hN‚ÇÅ'‚ü© := h‚ÇÅ N
    obtain ‚ü®N‚ÇÇ, hN‚ÇÇ, hN‚ÇÇ'‚ü© := h‚ÇÇ N‚ÇÅ
    exact ‚ü®N‚ÇÇ + 1, Nat.succ_le_of_lt <|
      lt_of_le_of_lt hN (upcrossingsBefore_lt_of_exists_upcrossing hab hN‚ÇÅ hN‚ÇÅ' hN‚ÇÇ hN‚ÇÇ')‚ü©
#align measure_theory.not_frequently_of_upcrossings_lt_top MeasureTheory.not_frequently_of_upcrossings_lt_top

/-- A stochastic process that frequently visits below `a` and above `b` has infinite upcrossings. -/
theorem upcrossings_eq_top_of_frequently_lt (hab : a < b) (h‚ÇÅ : ‚àÉ·∂† n in atTop, f n œâ < a)
    (h‚ÇÇ : ‚àÉ·∂† n in atTop, b < f n œâ) : upcrossings a b f œâ = ‚àû :=
  by_contradiction fun h => not_frequently_of_upcrossings_lt_top hab h ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©
#align measure_theory.upcrossings_eq_top_of_frequently_lt MeasureTheory.upcrossings_eq_top_of_frequently_lt

/-- A realization of a stochastic process with bounded upcrossings and bounded liminfs is
convergent.

We use the spelling `< ‚àû` instead of the standard `‚â† ‚àû` in the assumptions since it is not as easy
to change `<` to `‚â†` under binders. -/
theorem tendsto_of_uncrossing_lt_top (hf‚ÇÅ : liminf (fun n => (‚Äñf n œâ‚Äñ‚Çä : ‚Ñù‚â•0‚àû)) atTop < ‚àû)
    (hf‚ÇÇ : ‚àÄ a b : ‚Ñö, a < b ‚Üí upcrossings a b f œâ < ‚àû) :
    ‚àÉ c, Tendsto (fun n => f n œâ) atTop (ùìù c) := by
  by_cases h : IsBoundedUnder (¬∑ ‚â§ ¬∑) atTop fun n => |f n œâ|
  ¬∑ rw [isBoundedUnder_le_abs] at h
    refine' tendsto_of_no_upcrossings Rat.denseRange_cast _ h.1 h.2
    ¬∑ intro a ha b hb hab
      obtain ‚ü®‚ü®a, rfl‚ü©, ‚ü®b, rfl‚ü©‚ü© := ha, hb
      exact not_frequently_of_upcrossings_lt_top hab (hf‚ÇÇ a b (Rat.cast_lt.1 hab)).ne
  ¬∑ obtain ‚ü®a, b, hab, h‚ÇÅ, h‚ÇÇ‚ü© := ENNReal.exists_upcrossings_of_not_bounded_under hf‚ÇÅ.ne h
    exact
      False.elim ((hf‚ÇÇ a b hab).ne (upcrossings_eq_top_of_frequently_lt (Rat.cast_lt.2 hab) h‚ÇÅ h‚ÇÇ))
#align measure_theory.tendsto_of_uncrossing_lt_top MeasureTheory.tendsto_of_uncrossing_lt_top

/-- An L¬π-bounded submartingale has bounded upcrossings almost everywhere. -/
theorem Submartingale.upcrossings_ae_lt_top' [IsFiniteMeasure Œº] (hf : Submartingale f ‚Ñ± Œº)
    (hbdd : ‚àÄ n, snorm (f n) 1 Œº ‚â§ R) (hab : a < b) : ‚àÄ·µê œâ ‚àÇŒº, upcrossings a b f œâ < ‚àû := by
  refine' ae_lt_top (hf.adapted.measurable_upcrossings hab) _
  have := hf.mul_lintegral_upcrossings_le_lintegral_pos_part a b
  rw [mul_comm, ‚Üê ENNReal.le_div_iff_mul_le] at this
  ¬∑ refine' (lt_of_le_of_lt this (ENNReal.div_lt_top _ _)).ne
    ¬∑ have hR' : ‚àÄ n, ‚à´‚Åª œâ, ‚Äñf n œâ - a‚Äñ‚Çä ‚àÇŒº ‚â§ R + ‚Äña‚Äñ‚Çä * Œº Set.univ := by
        simp_rw [snorm_one_eq_lintegral_nnnorm] at hbdd
        intro n
        refine' (lintegral_mono _ : ‚à´‚Åª œâ, ‚Äñf n œâ - a‚Äñ‚Çä ‚àÇŒº ‚â§ ‚à´‚Åª œâ, ‚Äñf n œâ‚Äñ‚Çä + ‚Äña‚Äñ‚Çä ‚àÇŒº).trans _
        ¬∑ intro œâ
          simp_rw [sub_eq_add_neg, ‚Üê nnnorm_neg a, ‚Üê ENNReal.coe_add, ENNReal.coe_le_coe]
          exact nnnorm_add_le _ _
        ¬∑ simp_rw [lintegral_add_right _ measurable_const, lintegral_const]
          exact add_le_add (hbdd _) le_rfl
      refine' ne_of_lt (iSup_lt_iff.2 ‚ü®R + ‚Äña‚Äñ‚Çä * Œº Set.univ, ENNReal.add_lt_top.2
        ‚ü®ENNReal.coe_lt_top, ENNReal.mul_lt_top ENNReal.coe_lt_top.ne (measure_ne_top _ _)‚ü©,
        fun n => le_trans _ (hR' n)‚ü©)
      refine' lintegral_mono fun œâ => _
      rw [ENNReal.ofReal_le_iff_le_toReal, ENNReal.coe_toReal, coe_nnnorm]
      by_cases hnonneg : 0 ‚â§ f n œâ - a
      ¬∑ rw [LatticeOrderedGroup.pos_of_nonneg _ hnonneg, Real.norm_eq_abs,
          abs_of_nonneg hnonneg]
      ¬∑ rw [LatticeOrderedGroup.pos_of_nonpos _ (not_le.1 hnonneg).le]
        exact norm_nonneg _
      ¬∑ simp only [Ne.def, ENNReal.coe_ne_top, not_false_iff]
    ¬∑ simp only [hab, Ne.def, ENNReal.ofReal_eq_zero, sub_nonpos, not_le]
  ¬∑ simp only [hab, Ne.def, ENNReal.ofReal_eq_zero, sub_nonpos, not_le, true_or_iff]
  ¬∑ simp only [Ne.def, ENNReal.ofReal_ne_top, not_false_iff, true_or_iff]
#align measure_theory.submartingale.upcrossings_ae_lt_top' MeasureTheory.Submartingale.upcrossings_ae_lt_top'

theorem Submartingale.upcrossings_ae_lt_top [IsFiniteMeasure Œº] (hf : Submartingale f ‚Ñ± Œº)
    (hbdd : ‚àÄ n, snorm (f n) 1 Œº ‚â§ R) : ‚àÄ·µê œâ ‚àÇŒº, ‚àÄ a b : ‚Ñö, a < b ‚Üí upcrossings a b f œâ < ‚àû := by
  simp only [ae_all_iff, eventually_imp_distrib_left]
  rintro a b hab
  exact hf.upcrossings_ae_lt_top' hbdd (Rat.cast_lt.2 hab)
#align measure_theory.submartingale.upcrossings_ae_lt_top MeasureTheory.Submartingale.upcrossings_ae_lt_top

/-- An L¬π-bounded submartingale converges almost everywhere. -/
theorem Submartingale.exists_ae_tendsto_of_bdd [IsFiniteMeasure Œº] (hf : Submartingale f ‚Ñ± Œº)
    (hbdd : ‚àÄ n, snorm (f n) 1 Œº ‚â§ R) : ‚àÄ·µê œâ ‚àÇŒº, ‚àÉ c, Tendsto (fun n => f n œâ) atTop (ùìù c) := by
  filter_upwards [hf.upcrossings_ae_lt_top hbdd, ae_bdd_liminf_atTop_of_snorm_bdd one_ne_zero
    (fun n => (hf.stronglyMeasurable n).measurable.mono (‚Ñ±.le n) le_rfl) hbdd] with œâ h‚ÇÅ h‚ÇÇ
  exact tendsto_of_uncrossing_lt_top h‚ÇÇ h‚ÇÅ
#align measure_theory.submartingale.exists_ae_tendsto_of_bdd MeasureTheory.Submartingale.exists_ae_tendsto_of_bdd

theorem Submartingale.exists_ae_trim_tendsto_of_bdd [IsFiniteMeasure Œº] (hf : Submartingale f ‚Ñ± Œº)
    (hbdd : ‚àÄ n, snorm (f n) 1 Œº ‚â§ R) :
    ‚àÄ·µê œâ ‚àÇŒº.trim (sSup_le fun m ‚ü®n, hn‚ü© => hn ‚ñ∏ ‚Ñ±.le _ : ‚®Ü n, ‚Ñ± n ‚â§ m0),
      ‚àÉ c, Tendsto (fun n => f n œâ) atTop (ùìù c) := by
  letI := (‚®Ü n, ‚Ñ± n)
  rw [ae_iff, trim_measurableSet_eq]
  ¬∑ exact hf.exists_ae_tendsto_of_bdd hbdd
  ¬∑ exact MeasurableSet.compl $ measurableSet_exists_tendsto
      fun n => (hf.stronglyMeasurable n).measurable.mono (le_sSup ‚ü®n, rfl‚ü©) le_rfl
#align measure_theory.submartingale.exists_ae_trim_tendsto_of_bdd MeasureTheory.Submartingale.exists_ae_trim_tendsto_of_bdd

/-- **Almost everywhere martingale convergence theorem**: An L¬π-bounded submartingale converges
almost everywhere to a `‚®Ü n, ‚Ñ± n`-measurable function. -/
theorem Submartingale.ae_tendsto_limitProcess [IsFiniteMeasure Œº] (hf : Submartingale f ‚Ñ± Œº)
    (hbdd : ‚àÄ n, snorm (f n) 1 Œº ‚â§ R) :
    ‚àÄ·µê œâ ‚àÇŒº, Tendsto (fun n => f n œâ) atTop (ùìù (‚Ñ±.limitProcess f Œº œâ)) := by
  classical
  suffices
      ‚àÉ g, StronglyMeasurable[‚®Ü n, ‚Ñ± n] g ‚àß ‚àÄ·µê œâ ‚àÇŒº, Tendsto (fun n => f n œâ) atTop (ùìù (g œâ)) by
    rw [limitProcess, dif_pos this]
    exact (Classical.choose_spec this).2
  set g' : Œ© ‚Üí ‚Ñù := fun œâ => if h : ‚àÉ c, Tendsto (fun n => f n œâ) atTop (ùìù c) then h.choose else 0
  have hle : ‚®Ü n, ‚Ñ± n ‚â§ m0 := sSup_le fun m ‚ü®n, hn‚ü© => hn ‚ñ∏ ‚Ñ±.le _
  have hg' : ‚àÄ·µê œâ ‚àÇŒº.trim hle, Tendsto (fun n => f n œâ) atTop (ùìù (g' œâ)) := by
    filter_upwards [hf.exists_ae_trim_tendsto_of_bdd hbdd] with œâ hœâ
    simp_rw [dif_pos hœâ]
    exact hœâ.choose_spec
  have hg'm : @AEStronglyMeasurable _ _ _ (‚®Ü n, ‚Ñ± n) g' (Œº.trim hle) :=
    (@aemeasurable_of_tendsto_metrizable_ae' _ _ (‚®Ü n, ‚Ñ± n) _ _ _ _ _ _ _
      (fun n => ((hf.stronglyMeasurable n).measurable.mono (le_sSup ‚ü®n, rfl‚ü© : ‚Ñ± n ‚â§ ‚®Ü n, ‚Ñ± n)
        le_rfl).aemeasurable) hg').aestronglyMeasurable
  obtain ‚ü®g, hgm, hae‚ü© := hg'm
  have hg : ‚àÄ·µê œâ ‚àÇŒº.trim hle, Tendsto (fun n => f n œâ) atTop (ùìù (g œâ)) := by
    filter_upwards [hae, hg'] with œâ hœâ hg'œâ
    exact hœâ ‚ñ∏ hg'œâ
  exact ‚ü®g, hgm, measure_eq_zero_of_trim_eq_zero hle hg‚ü©
#align measure_theory.submartingale.ae_tendsto_limit_process MeasureTheory.Submartingale.ae_tendsto_limitProcess

/-- The limiting process of an L·µñ-bounded submartingale is L·µñ. -/
theorem Submartingale.mem‚Ñíp_limitProcess {p : ‚Ñù‚â•0‚àû} (hf : Submartingale f ‚Ñ± Œº)
    (hbdd : ‚àÄ n, snorm (f n) p Œº ‚â§ R) : Mem‚Ñíp (‚Ñ±.limitProcess f Œº) p Œº :=
  mem‚Ñíp_limitProcess_of_snorm_bdd
    (fun n => ((hf.stronglyMeasurable n).mono (‚Ñ±.le n)).aestronglyMeasurable) hbdd
#align measure_theory.submartingale.mem_‚Ñíp_limit_process MeasureTheory.Submartingale.mem‚Ñíp_limitProcess

end AeConvergence

section L1Convergence

variable [IsFiniteMeasure Œº] {g : Œ© ‚Üí ‚Ñù}

/-!

### L¬π martingale convergence theorem

We will now prove the L¬π martingale convergence theorems.

The L¬π martingale convergence theorem states that:
(a) if `f` is a uniformly integrable (in the probability sense) submartingale adapted to the
  filtration `‚Ñ±`, it converges in L¬π to an integrable function `g` which is measurable with
  respect to `‚Ñ±‚àû := ‚®Ü n, ‚Ñ± n` and
(b) if `f` is actually a martingale, `f n = ùîº[g | ‚Ñ± n]` almost everywhere.
(c) Finally, if `h` is integrable and measurable with respect to `‚Ñ±‚àû`, `(ùîº[h | ‚Ñ± n])‚Çô` is a
  uniformly integrable martingale which converges to `h` almost everywhere and in L¬π.

The proof is quite simple. (a) follows directly from the a.e. martingale convergence theorem
and the Vitali convergence theorem as our definition of uniform integrability (in the probability
sense) directly implies L¬π-uniform boundedness. We note that our definition of uniform
integrability is slightly non-standard but is equivalent to the usual literary definition. This
equivalence is provided by `MeasureTheory.uniformIntegrable_iff`.

(b) follows since given $n$, we have for all $m \ge n$,
$$
  \|f_n - \mathbb{E}[g \mid \mathcal{F}_n]\|_1 =
    \|\mathbb{E}[f_m - g \mid \mathcal{F}_n]\|_1 \le \|\|f_m - g\|_1.
$$
Thus, taking $m \to \infty$ provides the almost everywhere equality.

Finally, to prove (c), we define $f_n := \mathbb{E}[h \mid \mathcal{F}_n]$. It is clear that
$(f_n)_n$ is a martingale by the tower property for conditional expectations. Furthermore,
$(f_n)_n$ is uniformly integrable in the probability sense. Indeed, as a single function is
uniformly integrable in the measure theory sense, for all $\epsilon > 0$, there exists some
$\delta > 0$ such that for all measurable set $A$ with $\mu(A) < Œ¥$, we have
$\mathbb{E}|h|\mathbf{1}_A < \epsilon$. So, since for sufficently large $\lambda$, by the Markov
inequality, we have for all $n$,
$$
  \mu(|f_n| \ge \lambda) \le \lambda^{-1}\mathbb{E}|f_n| \le \lambda^{-1}\mathbb|g| < \delta,
$$
we have for sufficently large $\lambda$, for all $n$,
$$
  \mathbb{E}|f_n|\mathbf{1}_{|f_n| \ge \lambda} \le
    \mathbb|g|\mathbf{1}_{|f_n| \ge \lambda} < \epsilon,
$$
implying $(f_n)_n$ is uniformly integrable. Now, to prove $f_n \to h$ almost everywhere and in
L¬π, it suffices to show that $h = g$ almost everywhere where $g$ is the almost everywhere and L¬π
limit of $(f_n)_n$ from part (b) of the theorem. By noting that, for all $s \in \mathcal{F}_n$,
we have
$$
  \mathbb{E}g\mathbf{1}_s = \mathbb{E}[\mathbb{E}[g \mid \mathcal{F}_n]\mathbf{1}_s] =
    \mathbb{E}[\mathbb{E}[h \mid \mathcal{F}_n]\mathbf{1}_s] = \mathbb{E}h\mathbf{1}_s
$$
where $\mathbb{E}[g \mid \mathcal{F}_n = \mathbb{E}[h \mid \mathcal{F}_n]$ almost everywhere
by part (b); the equality also holds for all $s \in \mathcal{F}_\infty$ by Dynkin's theorem.
Thus, as both $h$ and $g$ are $\mathcal{F}_\infty$-measurable, $h = g$ almost everywhere as
required.

Similar to the a.e. martingale convergence theorem, rather than showing the existence of the
limiting process, we phrase the L¬π-martingale convergence theorem by proving that a submartingale
does converge in L¬π to its `limitProcess`. However, in contrast to the a.e. martingale convergence
theorem, we do not need to introduce an L¬π version of `Filtration.limitProcess` as the L¬π limit
and the a.e. limit of a submartingale coincide.

-/


/-- Part a of the **L¬π martingale convergence theorem**: a uniformly integrable submartingale
adapted to the filtration `‚Ñ±` converges a.e. and in L¬π to an integrable function which is
measurable with respect to the œÉ-algebra `‚®Ü n, ‚Ñ± n`. -/
theorem Submartingale.tendsto_snorm_one_limitProcess (hf : Submartingale f ‚Ñ± Œº)
    (hunif : UniformIntegrable f 1 Œº) :
    Tendsto (fun n => snorm (f n - ‚Ñ±.limitProcess f Œº) 1 Œº) atTop (ùìù 0) := by
  obtain ‚ü®R, hR‚ü© := hunif.2.2
  have hmeas : ‚àÄ n, AEStronglyMeasurable (f n) Œº := fun n =>
    ((hf.stronglyMeasurable n).mono (‚Ñ±.le _)).aestronglyMeasurable
  exact tendsto_Lp_of_tendstoInMeasure _ le_rfl ENNReal.one_ne_top hmeas
    (mem‚Ñíp_limitProcess_of_snorm_bdd hmeas hR) hunif.2.1
    (tendstoInMeasure_of_tendsto_ae hmeas <| hf.ae_tendsto_limitProcess hR)
#align measure_theory.submartingale.tendsto_snorm_one_limit_process MeasureTheory.Submartingale.tendsto_snorm_one_limitProcess

theorem Submartingale.ae_tendsto_limitProcess_of_uniformIntegrable (hf : Submartingale f ‚Ñ± Œº)
    (hunif : UniformIntegrable f 1 Œº) :
    ‚àÄ·µê œâ ‚àÇŒº, Tendsto (fun n => f n œâ) atTop (ùìù (‚Ñ±.limitProcess f Œº œâ)) :=
  let ‚ü®_, hR‚ü© := hunif.2.2
  hf.ae_tendsto_limitProcess hR
#align measure_theory.submartingale.ae_tendsto_limit_process_of_uniform_integrable MeasureTheory.Submartingale.ae_tendsto_limitProcess_of_uniformIntegrable

/-- If a martingale `f` adapted to `‚Ñ±` converges in L¬π to `g`, then for all `n`, `f n` is almost
everywhere equal to `ùîº[g | ‚Ñ± n]`. -/
theorem Martingale.eq_condexp_of_tendsto_snorm {Œº : Measure Œ©} (hf : Martingale f ‚Ñ± Œº)
    (hg : Integrable g Œº) (hgtends : Tendsto (fun n => snorm (f n - g) 1 Œº) atTop (ùìù 0)) (n : ‚Ñï) :
    f n =·µê[Œº] Œº[g|‚Ñ± n] := by
  rw [‚Üê sub_ae_eq_zero, ‚Üê snorm_eq_zero_iff (((hf.stronglyMeasurable n).mono (‚Ñ±.le _)).sub
    (stronglyMeasurable_condexp.mono (‚Ñ±.le _))).aestronglyMeasurable one_ne_zero]
  have ht : Tendsto (fun m => snorm (Œº[f m - g|‚Ñ± n]) 1 Œº) atTop (ùìù 0) :=
    haveI hint : ‚àÄ m, Integrable (f m - g) Œº := fun m => (hf.integrable m).sub hg
    tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds hgtends (fun m => zero_le _)
      fun m => snorm_one_condexp_le_snorm _
  have hev : ‚àÄ m ‚â• n, snorm (Œº[f m - g|‚Ñ± n]) 1 Œº = snorm (f n - Œº[g|‚Ñ± n]) 1 Œº := by
    refine' fun m hm => snorm_congr_ae ((condexp_sub (hf.integrable m) hg).trans _)
    filter_upwards [hf.2 n m hm] with x hx
    simp only [hx, Pi.sub_apply]
  exact tendsto_nhds_unique (tendsto_atTop_of_eventually_const hev) ht
#align measure_theory.martingale.eq_condexp_of_tendsto_snorm MeasureTheory.Martingale.eq_condexp_of_tendsto_snorm

/-- Part b of the **L¬π martingale convergence theorem**: if `f` is a uniformly integrable martingale
adapted to the filtration `‚Ñ±`, then for all `n`, `f n` is almost everywhere equal to the conditional
expectation of its limiting process wrt. `‚Ñ± n`. -/
theorem Martingale.ae_eq_condexp_limitProcess (hf : Martingale f ‚Ñ± Œº)
    (hbdd : UniformIntegrable f 1 Œº) (n : ‚Ñï) : f n =·µê[Œº] Œº[‚Ñ±.limitProcess f Œº|‚Ñ± n] :=
  let ‚ü®_, hR‚ü© := hbdd.2.2
  hf.eq_condexp_of_tendsto_snorm ((mem‚Ñíp_limitProcess_of_snorm_bdd hbdd.1 hR).integrable le_rfl)
    (hf.submartingale.tendsto_snorm_one_limitProcess hbdd) n
#align measure_theory.martingale.ae_eq_condexp_limit_process MeasureTheory.Martingale.ae_eq_condexp_limitProcess

/-- Part c of the **L¬π martingale convergence theorem**: Given an integrable function `g` which
is measurable with respect to `‚®Ü n, ‚Ñ± n` where `‚Ñ±` is a filtration, the martingale defined by
`ùîº[g | ‚Ñ± n]` converges almost everywhere to `g`.

This martingale also converges to `g` in L¬π and this result is provided by
`MeasureTheory.Integrable.tendsto_snorm_condexp` -/
theorem Integrable.tendsto_ae_condexp (hg : Integrable g Œº)
    (hgmeas : StronglyMeasurable[‚®Ü n, ‚Ñ± n] g) :
    ‚àÄ·µê x ‚àÇŒº, Tendsto (fun n => (Œº[g|‚Ñ± n]) x) atTop (ùìù (g x)) := by
  have hle : ‚®Ü n, ‚Ñ± n ‚â§ m0 := sSup_le fun m ‚ü®n, hn‚ü© => hn ‚ñ∏ ‚Ñ±.le _
  have hunif : UniformIntegrable (fun n => Œº[g|‚Ñ± n]) 1 Œº :=
    hg.uniformIntegrable_condexp_filtration
  obtain ‚ü®R, hR‚ü© := hunif.2.2
  have hlimint : Integrable (‚Ñ±.limitProcess (fun n => Œº[g|‚Ñ± n]) Œº) Œº :=
    (mem‚Ñíp_limitProcess_of_snorm_bdd hunif.1 hR).integrable le_rfl
  suffices g =·µê[Œº] ‚Ñ±.limitProcess (fun n x => (Œº[g|‚Ñ± n]) x) Œº by
    filter_upwards [this, (martingale_condexp g ‚Ñ± Œº).submartingale.ae_tendsto_limitProcess hR] with
      x heq ht
    rwa [heq]
  have : ‚àÄ n s, MeasurableSet[‚Ñ± n] s ‚Üí
      ‚à´ x in s, g x ‚àÇŒº = ‚à´ x in s, ‚Ñ±.limitProcess (fun n x => (Œº[g|‚Ñ± n]) x) Œº x ‚àÇŒº := by
    intro n s hs
    rw [‚Üê set_integral_condexp (‚Ñ±.le n) hg hs, ‚Üê set_integral_condexp (‚Ñ±.le n) hlimint hs]
    refine' set_integral_congr_ae (‚Ñ±.le _ _ hs) _
    filter_upwards [(martingale_condexp g ‚Ñ± Œº).ae_eq_condexp_limitProcess hunif n] with x hx _
    rw [hx]
  refine' ae_eq_of_forall_set_integral_eq_of_sigmaFinite' hle (fun s _ _ => hg.integrableOn)
    (fun s _ _ => hlimint.integrableOn) (fun s hs => _) hgmeas.aeStronglyMeasurable'
    stronglyMeasurable_limitProcess.aeStronglyMeasurable'
  apply @MeasurableSpace.induction_on_inter _ _ _ (‚®Ü n, ‚Ñ± n)
    (MeasurableSpace.measurableSpace_iSup_eq ‚Ñ±) _ _ _ _ _ _ hs
  ¬∑ rintro s ‚ü®n, hs‚ü© t ‚ü®m, ht‚ü© -
    by_cases hnm : n ‚â§ m
    ¬∑ exact ‚ü®m, (‚Ñ±.mono hnm _ hs).inter ht‚ü©
    ¬∑ exact ‚ü®n, hs.inter (‚Ñ±.mono (not_le.1 hnm).le _ ht)‚ü©
  ¬∑ simp only [measure_empty, ENNReal.zero_lt_top, Measure.restrict_empty, integral_zero_measure,
      forall_true_left]
  ¬∑ rintro t ‚ü®n, ht‚ü© -
    exact this n _ ht
  ¬∑ rintro t htmeas ht -
    have hgeq := @integral_add_compl _ _ (‚®Ü n, ‚Ñ± n) _ _ _ _ _ htmeas (hg.trim hle hgmeas)
    have hheq := @integral_add_compl _ _ (‚®Ü n, ‚Ñ± n) _ _ _ _ _ htmeas
      (hlimint.trim hle stronglyMeasurable_limitProcess)
    rw [add_comm, ‚Üê eq_sub_iff_add_eq] at hgeq hheq
    rw [set_integral_trim hle hgmeas htmeas.compl,
      set_integral_trim hle stronglyMeasurable_limitProcess htmeas.compl, hgeq, hheq, ‚Üê
      set_integral_trim hle hgmeas htmeas, ‚Üê
      set_integral_trim hle stronglyMeasurable_limitProcess htmeas, ‚Üê integral_trim hle hgmeas, ‚Üê
      integral_trim hle stronglyMeasurable_limitProcess, ‚Üê integral_univ,
      this 0 _ MeasurableSet.univ, integral_univ, ht (measure_lt_top _ _)]
  ¬∑ rintro f hf hfmeas heq -
    rw [integral_iUnion (fun n => hle _ (hfmeas n)) hf hg.integrableOn,
      integral_iUnion (fun n => hle _ (hfmeas n)) hf hlimint.integrableOn]
    exact tsum_congr fun n => heq _ (measure_lt_top _ _)
#align measure_theory.integrable.tendsto_ae_condexp MeasureTheory.Integrable.tendsto_ae_condexp

/-- Part c of the **L¬π martingale convergence theorem**: Given an integrable function `g` which
is measurable with respect to `‚®Ü n, ‚Ñ± n` where `‚Ñ±` is a filtration, the martingale defined by
`ùîº[g | ‚Ñ± n]` converges in L¬π to `g`.

This martingale also converges to `g` almost everywhere and this result is provided by
`MeasureTheory.Integrable.tendsto_ae_condexp` -/
theorem Integrable.tendsto_snorm_condexp (hg : Integrable g Œº)
    (hgmeas : StronglyMeasurable[‚®Ü n, ‚Ñ± n] g) :
    Tendsto (fun n => snorm (Œº[g|‚Ñ± n] - g) 1 Œº) atTop (ùìù 0) :=
  tendsto_Lp_of_tendstoInMeasure _ le_rfl ENNReal.one_ne_top
    (fun n => (stronglyMeasurable_condexp.mono (‚Ñ±.le n)).aestronglyMeasurable)
    (mem‚Ñíp_one_iff_integrable.2 hg) hg.uniformIntegrable_condexp_filtration.2.1
    (tendstoInMeasure_of_tendsto_ae
      (fun n => (stronglyMeasurable_condexp.mono (‚Ñ±.le n)).aestronglyMeasurable)
      (hg.tendsto_ae_condexp hgmeas))
#align measure_theory.integrable.tendsto_snorm_condexp MeasureTheory.Integrable.tendsto_snorm_condexp

/-- **L√©vy's upward theorem**, almost everywhere version: given a function `g` and a filtration
`‚Ñ±`, the sequence defined by `ùîº[g | ‚Ñ± n]` converges almost everywhere to `ùîº[g | ‚®Ü n, ‚Ñ± n]`. -/
theorem tendsto_ae_condexp (g : Œ© ‚Üí ‚Ñù) :
    ‚àÄ·µê x ‚àÇŒº, Tendsto (fun n => (Œº[g|‚Ñ± n]) x) atTop (ùìù ((Œº[g|‚®Ü n, ‚Ñ± n]) x)) := by
  have ht : ‚àÄ·µê x ‚àÇŒº, Tendsto (fun n => (Œº[Œº[g|‚®Ü n, ‚Ñ± n]|‚Ñ± n]) x) atTop (ùìù ((Œº[g|‚®Ü n, ‚Ñ± n]) x)) :=
    integrable_condexp.tendsto_ae_condexp stronglyMeasurable_condexp
  have heq : ‚àÄ n, ‚àÄ·µê x ‚àÇŒº, (Œº[Œº[g|‚®Ü n, ‚Ñ± n]|‚Ñ± n]) x = (Œº[g|‚Ñ± n]) x := fun n =>
    condexp_condexp_of_le (le_iSup _ n) (iSup_le fun n => ‚Ñ±.le n)
  rw [‚Üê ae_all_iff] at heq
  filter_upwards [heq, ht] with x hxeq hxt
  exact hxt.congr hxeq
#align measure_theory.tendsto_ae_condexp MeasureTheory.tendsto_ae_condexp

/-- **L√©vy's upward theorem**, L¬π version: given a function `g` and a filtration `‚Ñ±`, the
sequence defined by `ùîº[g | ‚Ñ± n]` converges in L¬π to `ùîº[g | ‚®Ü n, ‚Ñ± n]`. -/
theorem tendsto_snorm_condexp (g : Œ© ‚Üí ‚Ñù) :
    Tendsto (fun n => snorm (Œº[g|‚Ñ± n] - Œº[g|‚®Ü n, ‚Ñ± n]) 1 Œº) atTop (ùìù 0) := by
  have ht : Tendsto (fun n => snorm (Œº[Œº[g|‚®Ü n, ‚Ñ± n]|‚Ñ± n] - Œº[g|‚®Ü n, ‚Ñ± n]) 1 Œº) atTop (ùìù 0) :=
    integrable_condexp.tendsto_snorm_condexp stronglyMeasurable_condexp
  have heq : ‚àÄ n, ‚àÄ·µê x ‚àÇŒº, (Œº[Œº[g|‚®Ü n, ‚Ñ± n]|‚Ñ± n]) x = (Œº[g|‚Ñ± n]) x := fun n =>
    condexp_condexp_of_le (le_iSup _ n) (iSup_le fun n => ‚Ñ±.le n)
  refine' ht.congr fun n => snorm_congr_ae _
  filter_upwards [heq n] with x hxeq
  simp only [hxeq, Pi.sub_apply]
#align measure_theory.tendsto_snorm_condexp MeasureTheory.tendsto_snorm_condexp

end L1Convergence

end MeasureTheory
