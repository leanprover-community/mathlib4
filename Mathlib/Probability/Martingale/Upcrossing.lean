/-
Copyright (c) 2022 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import Mathlib.Order.Interval.Set.Monotone
import Mathlib.Probability.Process.HittingTime
import Mathlib.Probability.Martingale.Basic
import Mathlib.Tactic.AdaptationNote

#align_import probability.martingale.upcrossing from "leanprover-community/mathlib"@"2c1d8ca2812b64f88992a5294ea3dba144755cd1"

/-!

# Doob's upcrossing estimate

Given a discrete real-valued submartingale $(f_n)_{n \in \mathbb{N}}$, denoting by $U_N(a, b)$ the
number of times $f_n$ crossed from below $a$ to above $b$ before time $N$, Doob's upcrossing
estimate (also known as Doob's inequality) states that
$$(b - a) \mathbb{E}[U_N(a, b)] \le \mathbb{E}[(f_N - a)^+].$$
Doob's upcrossing estimate is an important inequality and is central in proving the martingale
convergence theorems.

## Main definitions

* `MeasureTheory.upperCrossingTime a b f N n`: is the stopping time corresponding to `f`
  crossing above `b` the `n`-th time before time `N` (if this does not occur then the value is
  taken to be `N`).
* `MeasureTheory.lowerCrossingTime a b f N n`: is the stopping time corresponding to `f`
  crossing below `a` the `n`-th time before time `N` (if this does not occur then the value is
  taken to be `N`).
* `MeasureTheory.upcrossingStrat a b f N`: is the predictable process which is 1 if `n` is
  between a consecutive pair of lower and upper crossings and is 0 otherwise. Intuitively
  one might think of the `upcrossingStrat` as the strategy of buying 1 share whenever the process
  crosses below `a` for the first time after selling and selling 1 share whenever the process
  crosses above `b` for the first time after buying.
* `MeasureTheory.upcrossingsBefore a b f N`: is the number of times `f` crosses from below `a` to
  above `b` before time `N`.
* `MeasureTheory.upcrossings a b f`: is the number of times `f` crosses from below `a` to above
  `b`. This takes value in `‚Ñù‚â•0‚àû` and so is allowed to be `‚àû`.

## Main results

* `MeasureTheory.Adapted.isStoppingTime_upperCrossingTime`: `upperCrossingTime` is a
  stopping time whenever the process it is associated to is adapted.
* `MeasureTheory.Adapted.isStoppingTime_lowerCrossingTime`: `lowerCrossingTime` is a
  stopping time whenever the process it is associated to is adapted.
* `MeasureTheory.Submartingale.mul_integral_upcrossingsBefore_le_integral_pos_part`: Doob's
  upcrossing estimate.
* `MeasureTheory.Submartingale.mul_lintegral_upcrossings_le_lintegral_pos_part`: the inequality
  obtained by taking the supremum on both sides of Doob's upcrossing estimate.

### References

We mostly follow the proof from [Kallenberg, *Foundations of modern probability*][kallenberg2021]

-/


open TopologicalSpace Filter

open scoped NNReal ENNReal MeasureTheory ProbabilityTheory Topology

namespace MeasureTheory

variable {Œ© Œπ : Type*} {m0 : MeasurableSpace Œ©} {Œº : Measure Œ©}

/-!

## Proof outline

In this section, we will denote by $U_N(a, b)$ the number of upcrossings of $(f_n)$ from below $a$
to above $b$ before time $N$.

To define $U_N(a, b)$, we will construct two stopping times corresponding to when $(f_n)$ crosses
below $a$ and above $b$. Namely, we define
$$
  \sigma_n := \inf \{n \ge \tau_n \mid f_n \le a\} \wedge N;
$$
$$
  \tau_{n + 1} := \inf \{n \ge \sigma_n \mid f_n \ge b\} \wedge N.
$$
These are `lowerCrossingTime` and `upperCrossingTime` in our formalization which are defined
using `MeasureTheory.hitting` allowing us to specify a starting and ending time.
Then, we may simply define $U_N(a, b) := \sup \{n \mid \tau_n < N\}$.

Fixing $a < b \in \mathbb{R}$, we will first prove the theorem in the special case that
$0 \le f_0$ and $a \le f_N$. In particular, we will show
$$
  (b - a) \mathbb{E}[U_N(a, b)] \le \mathbb{E}[f_N].
$$
This is `MeasureTheory.integral_mul_upcrossingsBefore_le_integral` in our formalization.

To prove this, we use the fact that given a non-negative, bounded, predictable process $(C_n)$
(i.e. $(C_{n + 1})$ is adapted), $(C \bullet f)_n := \sum_{k \le n} C_{k + 1}(f_{k + 1} - f_k)$ is
a submartingale if $(f_n)$ is.

Define $C_n := \sum_{k \le n} \mathbf{1}_{[\sigma_k, \tau_{k + 1})}(n)$. It is easy to see that
$(1 - C_n)$ is non-negative, bounded and predictable, and hence, given a submartingale $(f_n)$,
$(1 - C) \bullet f$ is also a submartingale. Thus, by the submartingale property,
$0 \le \mathbb{E}[((1 - C) \bullet f)_0] \le \mathbb{E}[((1 - C) \bullet f)_N]$ implying
$$
  \mathbb{E}[(C \bullet f)_N] \le \mathbb{E}[(1 \bullet f)_N] = \mathbb{E}[f_N] - \mathbb{E}[f_0].
$$

Furthermore,
\begin{align}
    (C \bullet f)_N & =
      \sum_{n \le N} \sum_{k \le N} \mathbf{1}_{[\sigma_k, \tau_{k + 1})}(n)(f_{n + 1} - f_n)\\
    & = \sum_{k \le N} \sum_{n \le N} \mathbf{1}_{[\sigma_k, \tau_{k + 1})}(n)(f_{n + 1} - f_n)\\
    & = \sum_{k \le N} (f_{\sigma_k + 1} - f_{\sigma_k} + f_{\sigma_k + 2} - f_{\sigma_k + 1}
      + \cdots + f_{\tau_{k + 1}} - f_{\tau_{k + 1} - 1})\\
    & = \sum_{k \le N} (f_{\tau_{k + 1}} - f_{\sigma_k})
      \ge \sum_{k < U_N(a, b)} (b - a) = (b - a) U_N(a, b)
\end{align}
where the inequality follows since for all $k < U_N(a, b)$,
$f_{\tau_{k + 1}} - f_{\sigma_k} \ge b - a$ while for all $k > U_N(a, b)$,
$f_{\tau_{k + 1}} = f_{\sigma_k} = f_N$ and
$f_{\tau_{U_N(a, b) + 1}} - f_{\sigma_{U_N(a, b)}} = f_N - a \ge 0$. Hence, we have
$$
  (b - a) \mathbb{E}[U_N(a, b)] \le \mathbb{E}[(C \bullet f)_N]
  \le \mathbb{E}[f_N] - \mathbb{E}[f_0] \le \mathbb{E}[f_N],
$$
as required.

To obtain the general case, we simply apply the above to $((f_n - a)^+)_n$.

-/


/-- `lowerCrossingTimeAux a f c N` is the first time `f` reached below `a` after time `c` before
time `N`. -/
noncomputable def lowerCrossingTimeAux [Preorder Œπ] [InfSet Œπ] (a : ‚Ñù) (f : Œπ ‚Üí Œ© ‚Üí ‚Ñù) (c N : Œπ) :
    Œ© ‚Üí Œπ :=
  hitting f (Set.Iic a) c N
#align measure_theory.lower_crossing_time_aux MeasureTheory.lowerCrossingTimeAux

/-- `upperCrossingTime a b f N n` is the first time before time `N`, `f` reaches
above `b` after `f` reached below `a` for the `n - 1`-th time. -/
noncomputable def upperCrossingTime [Preorder Œπ] [OrderBot Œπ] [InfSet Œπ] (a b : ‚Ñù) (f : Œπ ‚Üí Œ© ‚Üí ‚Ñù)
    (N : Œπ) : ‚Ñï ‚Üí Œ© ‚Üí Œπ
  | 0 => ‚ä•
  | n + 1 => fun œâ =>
    hitting f (Set.Ici b) (lowerCrossingTimeAux a f (upperCrossingTime a b f N n œâ) N œâ) N œâ
#align measure_theory.upper_crossing_time MeasureTheory.upperCrossingTime

/-- `lowerCrossingTime a b f N n` is the first time before time `N`, `f` reaches
below `a` after `f` reached above `b` for the `n`-th time. -/
noncomputable def lowerCrossingTime [Preorder Œπ] [OrderBot Œπ] [InfSet Œπ] (a b : ‚Ñù) (f : Œπ ‚Üí Œ© ‚Üí ‚Ñù)
    (N : Œπ) (n : ‚Ñï) : Œ© ‚Üí Œπ := fun œâ => hitting f (Set.Iic a) (upperCrossingTime a b f N n œâ) N œâ
#align measure_theory.lower_crossing_time MeasureTheory.lowerCrossingTime

section

variable [Preorder Œπ] [OrderBot Œπ] [InfSet Œπ]
variable {a b : ‚Ñù} {f : Œπ ‚Üí Œ© ‚Üí ‚Ñù} {N : Œπ} {n m : ‚Ñï} {œâ : Œ©}

@[simp]
theorem upperCrossingTime_zero : upperCrossingTime a b f N 0 = ‚ä• :=
  rfl
#align measure_theory.upper_crossing_time_zero MeasureTheory.upperCrossingTime_zero

@[simp]
theorem lowerCrossingTime_zero : lowerCrossingTime a b f N 0 = hitting f (Set.Iic a) ‚ä• N :=
  rfl
#align measure_theory.lower_crossing_time_zero MeasureTheory.lowerCrossingTime_zero

theorem upperCrossingTime_succ : upperCrossingTime a b f N (n + 1) œâ =
    hitting f (Set.Ici b) (lowerCrossingTimeAux a f (upperCrossingTime a b f N n œâ) N œâ) N œâ := by
  rw [upperCrossingTime]
#align measure_theory.upper_crossing_time_succ MeasureTheory.upperCrossingTime_succ

theorem upperCrossingTime_succ_eq (œâ : Œ©) : upperCrossingTime a b f N (n + 1) œâ =
    hitting f (Set.Ici b) (lowerCrossingTime a b f N n œâ) N œâ := by
  simp only [upperCrossingTime_succ]
  rfl
#align measure_theory.upper_crossing_time_succ_eq MeasureTheory.upperCrossingTime_succ_eq

end

section ConditionallyCompleteLinearOrderBot

variable [ConditionallyCompleteLinearOrderBot Œπ]
variable {a b : ‚Ñù} {f : Œπ ‚Üí Œ© ‚Üí ‚Ñù} {N : Œπ} {n m : ‚Ñï} {œâ : Œ©}

theorem upperCrossingTime_le : upperCrossingTime a b f N n œâ ‚â§ N := by
  cases n
  ¬∑ simp only [upperCrossingTime_zero, Pi.bot_apply, bot_le, Nat.zero_eq]
  ¬∑ simp only [upperCrossingTime_succ, hitting_le]
#align measure_theory.upper_crossing_time_le MeasureTheory.upperCrossingTime_le

@[simp]
theorem upperCrossingTime_zero' : upperCrossingTime a b f ‚ä• n œâ = ‚ä• :=
  eq_bot_iff.2 upperCrossingTime_le
#align measure_theory.upper_crossing_time_zero' MeasureTheory.upperCrossingTime_zero'

theorem lowerCrossingTime_le : lowerCrossingTime a b f N n œâ ‚â§ N := by
  simp only [lowerCrossingTime, hitting_le œâ]
#align measure_theory.lower_crossing_time_le MeasureTheory.lowerCrossingTime_le

theorem upperCrossingTime_le_lowerCrossingTime :
    upperCrossingTime a b f N n œâ ‚â§ lowerCrossingTime a b f N n œâ := by
  simp only [lowerCrossingTime, le_hitting upperCrossingTime_le œâ]
#align measure_theory.upper_crossing_time_le_lower_crossing_time MeasureTheory.upperCrossingTime_le_lowerCrossingTime

theorem lowerCrossingTime_le_upperCrossingTime_succ :
    lowerCrossingTime a b f N n œâ ‚â§ upperCrossingTime a b f N (n + 1) œâ := by
  rw [upperCrossingTime_succ]
  exact le_hitting lowerCrossingTime_le œâ
#align measure_theory.lower_crossing_time_le_upper_crossing_time_succ MeasureTheory.lowerCrossingTime_le_upperCrossingTime_succ

theorem lowerCrossingTime_mono (hnm : n ‚â§ m) :
    lowerCrossingTime a b f N n œâ ‚â§ lowerCrossingTime a b f N m œâ := by
  suffices Monotone fun n => lowerCrossingTime a b f N n œâ by exact this hnm
  exact monotone_nat_of_le_succ fun n =>
    le_trans lowerCrossingTime_le_upperCrossingTime_succ upperCrossingTime_le_lowerCrossingTime
#align measure_theory.lower_crossing_time_mono MeasureTheory.lowerCrossingTime_mono

theorem upperCrossingTime_mono (hnm : n ‚â§ m) :
    upperCrossingTime a b f N n œâ ‚â§ upperCrossingTime a b f N m œâ := by
  suffices Monotone fun n => upperCrossingTime a b f N n œâ by exact this hnm
  exact monotone_nat_of_le_succ fun n =>
    le_trans upperCrossingTime_le_lowerCrossingTime lowerCrossingTime_le_upperCrossingTime_succ
#align measure_theory.upper_crossing_time_mono MeasureTheory.upperCrossingTime_mono

end ConditionallyCompleteLinearOrderBot

variable {a b : ‚Ñù} {f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù} {N : ‚Ñï} {n m : ‚Ñï} {œâ : Œ©}

theorem stoppedValue_lowerCrossingTime (h : lowerCrossingTime a b f N n œâ ‚â† N) :
    stoppedValue f (lowerCrossingTime a b f N n) œâ ‚â§ a := by
  obtain ‚ü®j, hj‚ÇÅ, hj‚ÇÇ‚ü© := (hitting_le_iff_of_lt _ (lt_of_le_of_ne lowerCrossingTime_le h)).1 le_rfl
  exact stoppedValue_hitting_mem ‚ü®j, ‚ü®hj‚ÇÅ.1, le_trans hj‚ÇÅ.2 lowerCrossingTime_le‚ü©, hj‚ÇÇ‚ü©
#align measure_theory.stopped_value_lower_crossing_time MeasureTheory.stoppedValue_lowerCrossingTime

theorem stoppedValue_upperCrossingTime (h : upperCrossingTime a b f N (n + 1) œâ ‚â† N) :
    b ‚â§ stoppedValue f (upperCrossingTime a b f N (n + 1)) œâ := by
  obtain ‚ü®j, hj‚ÇÅ, hj‚ÇÇ‚ü© := (hitting_le_iff_of_lt _ (lt_of_le_of_ne upperCrossingTime_le h)).1 le_rfl
  exact stoppedValue_hitting_mem ‚ü®j, ‚ü®hj‚ÇÅ.1, le_trans hj‚ÇÅ.2 (hitting_le _)‚ü©, hj‚ÇÇ‚ü©
#align measure_theory.stopped_value_upper_crossing_time MeasureTheory.stoppedValue_upperCrossingTime

theorem upperCrossingTime_lt_lowerCrossingTime (hab : a < b)
    (hn : lowerCrossingTime a b f N (n + 1) œâ ‚â† N) :
    upperCrossingTime a b f N (n + 1) œâ < lowerCrossingTime a b f N (n + 1) œâ := by
  refine lt_of_le_of_ne upperCrossingTime_le_lowerCrossingTime fun h =>
    not_le.2 hab <| le_trans ?_ (stoppedValue_lowerCrossingTime hn)
  simp only [stoppedValue]
  rw [‚Üê h]
  exact stoppedValue_upperCrossingTime (h.symm ‚ñ∏ hn)
#align measure_theory.upper_crossing_time_lt_lower_crossing_time MeasureTheory.upperCrossingTime_lt_lowerCrossingTime

theorem lowerCrossingTime_lt_upperCrossingTime (hab : a < b)
    (hn : upperCrossingTime a b f N (n + 1) œâ ‚â† N) :
    lowerCrossingTime a b f N n œâ < upperCrossingTime a b f N (n + 1) œâ := by
  refine lt_of_le_of_ne lowerCrossingTime_le_upperCrossingTime_succ fun h =>
    not_le.2 hab <| le_trans (stoppedValue_upperCrossingTime hn) ?_
  simp only [stoppedValue]
  rw [‚Üê h]
  exact stoppedValue_lowerCrossingTime (h.symm ‚ñ∏ hn)
#align measure_theory.lower_crossing_time_lt_upper_crossing_time MeasureTheory.lowerCrossingTime_lt_upperCrossingTime

theorem upperCrossingTime_lt_succ (hab : a < b) (hn : upperCrossingTime a b f N (n + 1) œâ ‚â† N) :
    upperCrossingTime a b f N n œâ < upperCrossingTime a b f N (n + 1) œâ :=
  lt_of_le_of_lt upperCrossingTime_le_lowerCrossingTime
    (lowerCrossingTime_lt_upperCrossingTime hab hn)
#align measure_theory.upper_crossing_time_lt_succ MeasureTheory.upperCrossingTime_lt_succ

theorem lowerCrossingTime_stabilize (hnm : n ‚â§ m) (hn : lowerCrossingTime a b f N n œâ = N) :
    lowerCrossingTime a b f N m œâ = N :=
  le_antisymm lowerCrossingTime_le (le_trans (le_of_eq hn.symm) (lowerCrossingTime_mono hnm))
#align measure_theory.lower_crossing_time_stabilize MeasureTheory.lowerCrossingTime_stabilize

theorem upperCrossingTime_stabilize (hnm : n ‚â§ m) (hn : upperCrossingTime a b f N n œâ = N) :
    upperCrossingTime a b f N m œâ = N :=
  le_antisymm upperCrossingTime_le (le_trans (le_of_eq hn.symm) (upperCrossingTime_mono hnm))
#align measure_theory.upper_crossing_time_stabilize MeasureTheory.upperCrossingTime_stabilize

theorem lowerCrossingTime_stabilize' (hnm : n ‚â§ m) (hn : N ‚â§ lowerCrossingTime a b f N n œâ) :
    lowerCrossingTime a b f N m œâ = N :=
  lowerCrossingTime_stabilize hnm (le_antisymm lowerCrossingTime_le hn)
#align measure_theory.lower_crossing_time_stabilize' MeasureTheory.lowerCrossingTime_stabilize'

theorem upperCrossingTime_stabilize' (hnm : n ‚â§ m) (hn : N ‚â§ upperCrossingTime a b f N n œâ) :
    upperCrossingTime a b f N m œâ = N :=
  upperCrossingTime_stabilize hnm (le_antisymm upperCrossingTime_le hn)
#align measure_theory.upper_crossing_time_stabilize' MeasureTheory.upperCrossingTime_stabilize'

-- `upperCrossingTime_bound_eq` provides an explicit bound
theorem exists_upperCrossingTime_eq (f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù) (N : ‚Ñï) (œâ : Œ©) (hab : a < b) :
    ‚àÉ n, upperCrossingTime a b f N n œâ = N := by
  by_contra h; push_neg at h
  have : StrictMono fun n => upperCrossingTime a b f N n œâ :=
    strictMono_nat_of_lt_succ fun n => upperCrossingTime_lt_succ hab (h _)
  obtain ‚ü®_, ‚ü®k, rfl‚ü©, hk‚ü© :
      ‚àÉ (m : _) (_ : m ‚àà Set.range fun n => upperCrossingTime a b f N n œâ), N < m :=
    ‚ü®upperCrossingTime a b f N (N + 1) œâ, ‚ü®N + 1, rfl‚ü©,
      lt_of_lt_of_le N.lt_succ_self (StrictMono.id_le this (N + 1))‚ü©
  exact not_le.2 hk upperCrossingTime_le
#align measure_theory.exists_upper_crossing_time_eq MeasureTheory.exists_upperCrossingTime_eq

theorem upperCrossingTime_lt_bddAbove (hab : a < b) :
    BddAbove {n | upperCrossingTime a b f N n œâ < N} := by
  obtain ‚ü®k, hk‚ü© := exists_upperCrossingTime_eq f N œâ hab
  refine ‚ü®k, fun n (hn : upperCrossingTime a b f N n œâ < N) => ?_‚ü©
  by_contra hn'
  exact hn.ne (upperCrossingTime_stabilize (not_le.1 hn').le hk)
#align measure_theory.upper_crossing_time_lt_bdd_above MeasureTheory.upperCrossingTime_lt_bddAbove

theorem upperCrossingTime_lt_nonempty (hN : 0 < N) :
    {n | upperCrossingTime a b f N n œâ < N}.Nonempty :=
  ‚ü®0, hN‚ü©
#align measure_theory.upper_crossing_time_lt_nonempty MeasureTheory.upperCrossingTime_lt_nonempty

theorem upperCrossingTime_bound_eq (f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù) (N : ‚Ñï) (œâ : Œ©) (hab : a < b) :
    upperCrossingTime a b f N N œâ = N := by
  by_cases hN' : N < Nat.find (exists_upperCrossingTime_eq f N œâ hab)
  ¬∑ refine le_antisymm upperCrossingTime_le ?_
    have hmono : StrictMonoOn (fun n => upperCrossingTime a b f N n œâ)
        (Set.Iic (Nat.find (exists_upperCrossingTime_eq f N œâ hab)).pred) := by
      refine strictMonoOn_Iic_of_lt_succ fun m hm => upperCrossingTime_lt_succ hab ?_
      rw [Nat.lt_pred_iff] at hm
      convert Nat.find_min _ hm
    convert StrictMonoOn.Iic_id_le hmono N (Nat.le_sub_one_of_lt hN')
  ¬∑ rw [not_lt] at hN'
    exact upperCrossingTime_stabilize hN' (Nat.find_spec (exists_upperCrossingTime_eq f N œâ hab))
#align measure_theory.upper_crossing_time_bound_eq MeasureTheory.upperCrossingTime_bound_eq

theorem upperCrossingTime_eq_of_bound_le (hab : a < b) (hn : N ‚â§ n) :
    upperCrossingTime a b f N n œâ = N :=
  le_antisymm upperCrossingTime_le
    (le_trans (upperCrossingTime_bound_eq f N œâ hab).symm.le (upperCrossingTime_mono hn))
#align measure_theory.upper_crossing_time_eq_of_bound_le MeasureTheory.upperCrossingTime_eq_of_bound_le

variable {‚Ñ± : Filtration ‚Ñï m0}

theorem Adapted.isStoppingTime_crossing (hf : Adapted ‚Ñ± f) :
    IsStoppingTime ‚Ñ± (upperCrossingTime a b f N n) ‚àß
      IsStoppingTime ‚Ñ± (lowerCrossingTime a b f N n) := by
  induction' n with k ih
  ¬∑ refine ‚ü®isStoppingTime_const _ 0, ?_‚ü©
    simp [hitting_isStoppingTime hf measurableSet_Iic]
  ¬∑ obtain ‚ü®_, ih‚ÇÇ‚ü© := ih
    have : IsStoppingTime ‚Ñ± (upperCrossingTime a b f N (k + 1)) := by
      intro n
      simp_rw [upperCrossingTime_succ_eq]
      exact isStoppingTime_hitting_isStoppingTime ih‚ÇÇ (fun _ => lowerCrossingTime_le)
        measurableSet_Ici hf _
    refine ‚ü®this, ?_‚ü©
    intro n
    exact isStoppingTime_hitting_isStoppingTime this (fun _ => upperCrossingTime_le)
      measurableSet_Iic hf _
#align measure_theory.adapted.is_stopping_time_crossing MeasureTheory.Adapted.isStoppingTime_crossing

theorem Adapted.isStoppingTime_upperCrossingTime (hf : Adapted ‚Ñ± f) :
    IsStoppingTime ‚Ñ± (upperCrossingTime a b f N n) :=
  hf.isStoppingTime_crossing.1
#align measure_theory.adapted.is_stopping_time_upper_crossing_time MeasureTheory.Adapted.isStoppingTime_upperCrossingTime

theorem Adapted.isStoppingTime_lowerCrossingTime (hf : Adapted ‚Ñ± f) :
    IsStoppingTime ‚Ñ± (lowerCrossingTime a b f N n) :=
  hf.isStoppingTime_crossing.2
#align measure_theory.adapted.is_stopping_time_lower_crossing_time MeasureTheory.Adapted.isStoppingTime_lowerCrossingTime

/-- `upcrossingStrat a b f N n` is 1 if `n` is between a consecutive pair of lower and upper
crossings and is 0 otherwise. `upcrossingStrat` is shifted by one index so that it is adapted
rather than predictable. -/
noncomputable def upcrossingStrat (a b : ‚Ñù) (f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù) (N n : ‚Ñï) (œâ : Œ©) : ‚Ñù :=
  ‚àë k ‚àà Finset.range N,
    (Set.Ico (lowerCrossingTime a b f N k œâ) (upperCrossingTime a b f N (k + 1) œâ)).indicator 1 n
#align measure_theory.upcrossing_strat MeasureTheory.upcrossingStrat

theorem upcrossingStrat_nonneg : 0 ‚â§ upcrossingStrat a b f N n œâ :=
  Finset.sum_nonneg fun _ _ => Set.indicator_nonneg (fun _ _ => zero_le_one) _
#align measure_theory.upcrossing_strat_nonneg MeasureTheory.upcrossingStrat_nonneg

theorem upcrossingStrat_le_one : upcrossingStrat a b f N n œâ ‚â§ 1 := by
  rw [upcrossingStrat, ‚Üê Finset.indicator_biUnion_apply]
  ¬∑ exact Set.indicator_le_self' (fun _ _ => zero_le_one) _
  intro i _ j _ hij
  simp only [Set.Ico_disjoint_Ico]
  obtain hij' | hij' := lt_or_gt_of_ne hij
  ¬∑ rw [min_eq_left (upperCrossingTime_mono (Nat.succ_le_succ hij'.le) :
      upperCrossingTime a b f N _ œâ ‚â§ upperCrossingTime a b f N _ œâ),
      max_eq_right (lowerCrossingTime_mono hij'.le :
        lowerCrossingTime a b f N _ _ ‚â§ lowerCrossingTime _ _ _ _ _ _)]
    refine le_trans upperCrossingTime_le_lowerCrossingTime
      (lowerCrossingTime_mono (Nat.succ_le_of_lt hij'))
  ¬∑ rw [gt_iff_lt] at hij'
    rw [min_eq_right (upperCrossingTime_mono (Nat.succ_le_succ hij'.le) :
      upperCrossingTime a b f N _ œâ ‚â§ upperCrossingTime a b f N _ œâ),
      max_eq_left (lowerCrossingTime_mono hij'.le :
        lowerCrossingTime a b f N _ _ ‚â§ lowerCrossingTime _ _ _ _ _ _)]
    refine le_trans upperCrossingTime_le_lowerCrossingTime
      (lowerCrossingTime_mono (Nat.succ_le_of_lt hij'))
#align measure_theory.upcrossing_strat_le_one MeasureTheory.upcrossingStrat_le_one

theorem Adapted.upcrossingStrat_adapted (hf : Adapted ‚Ñ± f) :
    Adapted ‚Ñ± (upcrossingStrat a b f N) := by
  intro n
  change StronglyMeasurable[‚Ñ± n] fun œâ =>
    ‚àë k ‚àà Finset.range N, ({n | lowerCrossingTime a b f N k œâ ‚â§ n} ‚à©
      {n | n < upperCrossingTime a b f N (k + 1) œâ}).indicator 1 n
  refine Finset.stronglyMeasurable_sum _ fun i _ =>
    stronglyMeasurable_const.indicator ((hf.isStoppingTime_lowerCrossingTime n).inter ?_)
  simp_rw [‚Üê not_le]
  exact (hf.isStoppingTime_upperCrossingTime n).compl
#align measure_theory.adapted.upcrossing_strat_adapted MeasureTheory.Adapted.upcrossingStrat_adapted

theorem Submartingale.sum_upcrossingStrat_mul [IsFiniteMeasure Œº] (hf : Submartingale f ‚Ñ± Œº)
    (a b : ‚Ñù) (N : ‚Ñï) : Submartingale (fun n : ‚Ñï =>
      ‚àë k ‚àà Finset.range n, upcrossingStrat a b f N k * (f (k + 1) - f k)) ‚Ñ± Œº :=
  hf.sum_mul_sub hf.adapted.upcrossingStrat_adapted (fun _ _ => upcrossingStrat_le_one) fun _ _ =>
    upcrossingStrat_nonneg
#align measure_theory.submartingale.sum_upcrossing_strat_mul MeasureTheory.Submartingale.sum_upcrossingStrat_mul

theorem Submartingale.sum_sub_upcrossingStrat_mul [IsFiniteMeasure Œº] (hf : Submartingale f ‚Ñ± Œº)
    (a b : ‚Ñù) (N : ‚Ñï) : Submartingale (fun n : ‚Ñï =>
      ‚àë k ‚àà Finset.range n, (1 - upcrossingStrat a b f N k) * (f (k + 1) - f k)) ‚Ñ± Œº := by
  refine hf.sum_mul_sub (fun n => (adapted_const ‚Ñ± 1 n).sub (hf.adapted.upcrossingStrat_adapted n))
    (?_ : ‚àÄ n œâ, (1 - upcrossingStrat a b f N n) œâ ‚â§ 1) ?_
  ¬∑ exact fun n œâ => sub_le_self _ upcrossingStrat_nonneg
  ¬∑ intro n œâ
    simp [upcrossingStrat_le_one]
#align measure_theory.submartingale.sum_sub_upcrossing_strat_mul MeasureTheory.Submartingale.sum_sub_upcrossingStrat_mul

theorem Submartingale.sum_mul_upcrossingStrat_le [IsFiniteMeasure Œº] (hf : Submartingale f ‚Ñ± Œº) :
    Œº[‚àë k ‚àà Finset.range n, upcrossingStrat a b f N k * (f (k + 1) - f k)] ‚â§ Œº[f n] - Œº[f 0] := by
  have h‚ÇÅ : (0 : ‚Ñù) ‚â§
      Œº[‚àë k ‚àà Finset.range n, (1 - upcrossingStrat a b f N k) * (f (k + 1) - f k)] := by
    have := (hf.sum_sub_upcrossingStrat_mul a b N).setIntegral_le (zero_le n) MeasurableSet.univ
    rw [integral_univ, integral_univ] at this
    refine le_trans ?_ this
    simp only [Finset.range_zero, Finset.sum_empty, integral_zero', le_refl]
  have h‚ÇÇ : Œº[‚àë k ‚àà Finset.range n, (1 - upcrossingStrat a b f N k) * (f (k + 1) - f k)] =
    Œº[‚àë k ‚àà Finset.range n, (f (k + 1) - f k)] -
      Œº[‚àë k ‚àà Finset.range n, upcrossingStrat a b f N k * (f (k + 1) - f k)] := by
    simp only [sub_mul, one_mul, Finset.sum_sub_distrib, Pi.sub_apply, Finset.sum_apply,
      Pi.mul_apply]
    refine integral_sub (Integrable.sub (integrable_finset_sum _ fun i _ => hf.integrable _)
      (integrable_finset_sum _ fun i _ => hf.integrable _)) ?_
    convert (hf.sum_upcrossingStrat_mul a b N).integrable n using 1
    ext; simp
  rw [h‚ÇÇ, sub_nonneg] at h‚ÇÅ
  refine le_trans h‚ÇÅ ?_
  simp_rw [Finset.sum_range_sub, integral_sub' (hf.integrable _) (hf.integrable _), le_refl]
#align measure_theory.submartingale.sum_mul_upcrossing_strat_le MeasureTheory.Submartingale.sum_mul_upcrossingStrat_le

/-- The number of upcrossings (strictly) before time `N`. -/
noncomputable def upcrossingsBefore [Preorder Œπ] [OrderBot Œπ] [InfSet Œπ] (a b : ‚Ñù) (f : Œπ ‚Üí Œ© ‚Üí ‚Ñù)
    (N : Œπ) (œâ : Œ©) : ‚Ñï :=
  sSup {n | upperCrossingTime a b f N n œâ < N}
#align measure_theory.upcrossings_before MeasureTheory.upcrossingsBefore

@[simp]
theorem upcrossingsBefore_bot [Preorder Œπ] [OrderBot Œπ] [InfSet Œπ] {a b : ‚Ñù} {f : Œπ ‚Üí Œ© ‚Üí ‚Ñù}
    {œâ : Œ©} : upcrossingsBefore a b f ‚ä• œâ = ‚ä• := by simp [upcrossingsBefore]
#align measure_theory.upcrossings_before_bot MeasureTheory.upcrossingsBefore_bot

theorem upcrossingsBefore_zero : upcrossingsBefore a b f 0 œâ = 0 := by simp [upcrossingsBefore]
#align measure_theory.upcrossings_before_zero MeasureTheory.upcrossingsBefore_zero

@[simp]
theorem upcrossingsBefore_zero' : upcrossingsBefore a b f 0 = 0 := by
  ext œâ; exact upcrossingsBefore_zero
#align measure_theory.upcrossings_before_zero' MeasureTheory.upcrossingsBefore_zero'

theorem upperCrossingTime_lt_of_le_upcrossingsBefore (hN : 0 < N) (hab : a < b)
    (hn : n ‚â§ upcrossingsBefore a b f N œâ) : upperCrossingTime a b f N n œâ < N :=
  haveI : upperCrossingTime a b f N (upcrossingsBefore a b f N œâ) œâ < N :=
    (upperCrossingTime_lt_nonempty hN).csSup_mem
      ((OrderBot.bddBelow _).finite_of_bddAbove (upperCrossingTime_lt_bddAbove hab))
  lt_of_le_of_lt (upperCrossingTime_mono hn) this
#align measure_theory.upper_crossing_time_lt_of_le_upcrossings_before MeasureTheory.upperCrossingTime_lt_of_le_upcrossingsBefore

theorem upperCrossingTime_eq_of_upcrossingsBefore_lt (hab : a < b)
    (hn : upcrossingsBefore a b f N œâ < n) : upperCrossingTime a b f N n œâ = N := by
  refine le_antisymm upperCrossingTime_le (not_lt.1 ?_)
  convert not_mem_of_csSup_lt hn (upperCrossingTime_lt_bddAbove hab)
#align measure_theory.upper_crossing_time_eq_of_upcrossings_before_lt MeasureTheory.upperCrossingTime_eq_of_upcrossingsBefore_lt

theorem upcrossingsBefore_le (f : ‚Ñï ‚Üí Œ© ‚Üí ‚Ñù) (œâ : Œ©) (hab : a < b) :
    upcrossingsBefore a b f N œâ ‚â§ N := by
  by_cases hN : N = 0
  ¬∑ subst hN
    rw [upcrossingsBefore_zero]
  ¬∑ refine csSup_le ‚ü®0, zero_lt_iff.2 hN‚ü© fun n (hn : _ < N) => ?_
    by_contra hnN
    exact hn.ne (upperCrossingTime_eq_of_bound_le hab (not_le.1 hnN).le)
#align measure_theory.upcrossings_before_le MeasureTheory.upcrossingsBefore_le

theorem crossing_eq_crossing_of_lowerCrossingTime_lt {M : ‚Ñï} (hNM : N ‚â§ M)
    (h : lowerCrossingTime a b f N n œâ < N) :
    upperCrossingTime a b f M n œâ = upperCrossingTime a b f N n œâ ‚àß
      lowerCrossingTime a b f M n œâ = lowerCrossingTime a b f N n œâ := by
  have h' : upperCrossingTime a b f N n œâ < N :=
    lt_of_le_of_lt upperCrossingTime_le_lowerCrossingTime h
  induction' n with k ih
  ¬∑ simp only [Nat.zero_eq, upperCrossingTime_zero, bot_eq_zero', eq_self_iff_true,
      lowerCrossingTime_zero, true_and_iff, eq_comm]
    refine hitting_eq_hitting_of_exists hNM ?_
    rw [lowerCrossingTime, hitting_lt_iff] at h
    ¬∑ obtain ‚ü®j, hj‚ÇÅ, hj‚ÇÇ‚ü© := h
      exact ‚ü®j, ‚ü®hj‚ÇÅ.1, hj‚ÇÅ.2.le‚ü©, hj‚ÇÇ‚ü©
    ¬∑ exact le_rfl
  ¬∑ specialize ih (lt_of_le_of_lt (lowerCrossingTime_mono (Nat.le_succ _)) h)
      (lt_of_le_of_lt (upperCrossingTime_mono (Nat.le_succ _)) h')
    have : upperCrossingTime a b f M k.succ œâ = upperCrossingTime a b f N k.succ œâ := by
      rw [upperCrossingTime_succ_eq, hitting_lt_iff] at h'
      ¬∑ simp only [upperCrossingTime_succ_eq]
        obtain ‚ü®j, hj‚ÇÅ, hj‚ÇÇ‚ü© := h'
        rw [eq_comm, ih.2]
        exact hitting_eq_hitting_of_exists hNM ‚ü®j, ‚ü®hj‚ÇÅ.1, hj‚ÇÅ.2.le‚ü©, hj‚ÇÇ‚ü©
      ¬∑ exact le_rfl
    refine ‚ü®this, ?_‚ü©
    simp only [lowerCrossingTime, eq_comm, this, Nat.succ_eq_add_one]
    refine hitting_eq_hitting_of_exists hNM ?_
    rw [lowerCrossingTime, hitting_lt_iff _ le_rfl] at h
    obtain ‚ü®j, hj‚ÇÅ, hj‚ÇÇ‚ü© := h
    exact ‚ü®j, ‚ü®hj‚ÇÅ.1, hj‚ÇÅ.2.le‚ü©, hj‚ÇÇ‚ü©
#align measure_theory.crossing_eq_crossing_of_lower_crossing_time_lt MeasureTheory.crossing_eq_crossing_of_lowerCrossingTime_lt

theorem crossing_eq_crossing_of_upperCrossingTime_lt {M : ‚Ñï} (hNM : N ‚â§ M)
    (h : upperCrossingTime a b f N (n + 1) œâ < N) :
    upperCrossingTime a b f M (n + 1) œâ = upperCrossingTime a b f N (n + 1) œâ ‚àß
      lowerCrossingTime a b f M n œâ = lowerCrossingTime a b f N n œâ := by
  have := (crossing_eq_crossing_of_lowerCrossingTime_lt hNM
    (lt_of_le_of_lt lowerCrossingTime_le_upperCrossingTime_succ h)).2
  refine ‚ü®?_, this‚ü©
  rw [upperCrossingTime_succ_eq, upperCrossingTime_succ_eq, eq_comm, this]
  refine hitting_eq_hitting_of_exists hNM ?_
  rw [upperCrossingTime_succ_eq, hitting_lt_iff] at h
  ¬∑ obtain ‚ü®j, hj‚ÇÅ, hj‚ÇÇ‚ü© := h
    exact ‚ü®j, ‚ü®hj‚ÇÅ.1, hj‚ÇÅ.2.le‚ü©, hj‚ÇÇ‚ü©
  ¬∑ exact le_rfl
#align measure_theory.crossing_eq_crossing_of_upper_crossing_time_lt MeasureTheory.crossing_eq_crossing_of_upperCrossingTime_lt

theorem upperCrossingTime_eq_upperCrossingTime_of_lt {M : ‚Ñï} (hNM : N ‚â§ M)
    (h : upperCrossingTime a b f N n œâ < N) :
    upperCrossingTime a b f M n œâ = upperCrossingTime a b f N n œâ := by
  cases n
  ¬∑ simp
  ¬∑ exact (crossing_eq_crossing_of_upperCrossingTime_lt hNM h).1
#align measure_theory.upper_crossing_time_eq_upper_crossing_time_of_lt MeasureTheory.upperCrossingTime_eq_upperCrossingTime_of_lt

theorem upcrossingsBefore_mono (hab : a < b) : Monotone fun N œâ => upcrossingsBefore a b f N œâ := by
  intro N M hNM œâ
  simp only [upcrossingsBefore]
  by_cases hemp : {n : ‚Ñï | upperCrossingTime a b f N n œâ < N}.Nonempty
  ¬∑ refine csSup_le_csSup (upperCrossingTime_lt_bddAbove hab) hemp fun n hn => ?_
    rw [Set.mem_setOf_eq, upperCrossingTime_eq_upperCrossingTime_of_lt hNM hn]
    exact lt_of_lt_of_le hn hNM
  ¬∑ rw [Set.not_nonempty_iff_eq_empty] at hemp
    simp [hemp, csSup_empty, bot_eq_zero', zero_le']
#align measure_theory.upcrossings_before_mono MeasureTheory.upcrossingsBefore_mono

theorem upcrossingsBefore_lt_of_exists_upcrossing (hab : a < b) {N‚ÇÅ N‚ÇÇ : ‚Ñï} (hN‚ÇÅ : N ‚â§ N‚ÇÅ)
    (hN‚ÇÅ' : f N‚ÇÅ œâ < a) (hN‚ÇÇ : N‚ÇÅ ‚â§ N‚ÇÇ) (hN‚ÇÇ' : b < f N‚ÇÇ œâ) :
    upcrossingsBefore a b f N œâ < upcrossingsBefore a b f (N‚ÇÇ + 1) œâ := by
  refine lt_of_lt_of_le (Nat.lt_succ_self _) (le_csSup (upperCrossingTime_lt_bddAbove hab) ?_)
  rw [Set.mem_setOf_eq, upperCrossingTime_succ_eq, hitting_lt_iff _ le_rfl]
  refine ‚ü®N‚ÇÇ, ‚ü®?_, Nat.lt_succ_self _‚ü©, hN‚ÇÇ'.le‚ü©
  rw [lowerCrossingTime, hitting_le_iff_of_lt _ (Nat.lt_succ_self _)]
  refine ‚ü®N‚ÇÅ, ‚ü®le_trans ?_ hN‚ÇÅ, hN‚ÇÇ‚ü©, hN‚ÇÅ'.le‚ü©
  by_cases hN : 0 < N
  ¬∑ have : upperCrossingTime a b f N (upcrossingsBefore a b f N œâ) œâ < N :=
      Nat.sSup_mem (upperCrossingTime_lt_nonempty hN) (upperCrossingTime_lt_bddAbove hab)
    rw [upperCrossingTime_eq_upperCrossingTime_of_lt (hN‚ÇÅ.trans (hN‚ÇÇ.trans <| Nat.le_succ _))
      this]
    exact this.le
  ¬∑ rw [not_lt, Nat.le_zero] at hN
    rw [hN, upcrossingsBefore_zero, upperCrossingTime_zero]
    rfl
#align measure_theory.upcrossings_before_lt_of_exists_upcrossing MeasureTheory.upcrossingsBefore_lt_of_exists_upcrossing

theorem lowerCrossingTime_lt_of_lt_upcrossingsBefore (hN : 0 < N) (hab : a < b)
    (hn : n < upcrossingsBefore a b f N œâ) : lowerCrossingTime a b f N n œâ < N :=
  lt_of_le_of_lt lowerCrossingTime_le_upperCrossingTime_succ
    (upperCrossingTime_lt_of_le_upcrossingsBefore hN hab hn)
#align measure_theory.lower_crossing_time_lt_of_lt_upcrossings_before MeasureTheory.lowerCrossingTime_lt_of_lt_upcrossingsBefore

theorem le_sub_of_le_upcrossingsBefore (hN : 0 < N) (hab : a < b)
    (hn : n < upcrossingsBefore a b f N œâ) :
    b - a ‚â§ stoppedValue f (upperCrossingTime a b f N (n + 1)) œâ -
      stoppedValue f (lowerCrossingTime a b f N n) œâ :=
  sub_le_sub
    (stoppedValue_upperCrossingTime (upperCrossingTime_lt_of_le_upcrossingsBefore hN hab hn).ne)
    (stoppedValue_lowerCrossingTime (lowerCrossingTime_lt_of_lt_upcrossingsBefore hN hab hn).ne)
#align measure_theory.le_sub_of_le_upcrossings_before MeasureTheory.le_sub_of_le_upcrossingsBefore

theorem sub_eq_zero_of_upcrossingsBefore_lt (hab : a < b) (hn : upcrossingsBefore a b f N œâ < n) :
    stoppedValue f (upperCrossingTime a b f N (n + 1)) œâ -
      stoppedValue f (lowerCrossingTime a b f N n) œâ = 0 := by
  have : N ‚â§ upperCrossingTime a b f N n œâ := by
    rw [upcrossingsBefore] at hn
    rw [‚Üê not_lt]
    exact fun h => not_le.2 hn (le_csSup (upperCrossingTime_lt_bddAbove hab) h)
  simp [stoppedValue, upperCrossingTime_stabilize' (Nat.le_succ n) this,
    lowerCrossingTime_stabilize' le_rfl (le_trans this upperCrossingTime_le_lowerCrossingTime)]
#align measure_theory.sub_eq_zero_of_upcrossings_before_lt MeasureTheory.sub_eq_zero_of_upcrossingsBefore_lt

theorem mul_upcrossingsBefore_le (hf : a ‚â§ f N œâ) (hab : a < b) :
    (b - a) * upcrossingsBefore a b f N œâ ‚â§
    ‚àë k ‚àà Finset.range N, upcrossingStrat a b f N k œâ * (f (k + 1) - f k) œâ := by
  classical
  by_cases hN : N = 0
  ¬∑ simp [hN]
  simp_rw [upcrossingStrat, Finset.sum_mul, ‚Üê
    Set.indicator_mul_left _ _ (fun x ‚Ü¶ (f (x + 1) - f x) œâ), Pi.one_apply, Pi.sub_apply, one_mul]
  rw [Finset.sum_comm]
  have h‚ÇÅ : ‚àÄ k, ‚àë n ‚àà Finset.range N, (Set.Ico (lowerCrossingTime a b f N k œâ)
      (upperCrossingTime a b f N (k + 1) œâ)).indicator (fun m => f (m + 1) œâ - f m œâ) n =
      stoppedValue f (upperCrossingTime a b f N (k + 1)) œâ -
        stoppedValue f (lowerCrossingTime a b f N k) œâ := by
    intro k
    rw [Finset.sum_indicator_eq_sum_filter, (_ : Finset.filter (fun i => i ‚àà Set.Ico
      (lowerCrossingTime a b f N k œâ) (upperCrossingTime a b f N (k + 1) œâ)) (Finset.range N) =
      Finset.Ico (lowerCrossingTime a b f N k œâ) (upperCrossingTime a b f N (k + 1) œâ)),
      Finset.sum_Ico_eq_add_neg _ lowerCrossingTime_le_upperCrossingTime_succ,
      Finset.sum_range_sub fun n => f n œâ, Finset.sum_range_sub fun n => f n œâ, neg_sub,
      sub_add_sub_cancel]
    ¬∑ rfl
    ¬∑ ext i
      simp only [Set.mem_Ico, Finset.mem_filter, Finset.mem_range, Finset.mem_Ico,
        and_iff_right_iff_imp, and_imp]
      exact fun _ h => lt_of_lt_of_le h upperCrossingTime_le
  simp_rw [h‚ÇÅ]
  have h‚ÇÇ : ‚àë _k ‚àà Finset.range (upcrossingsBefore a b f N œâ), (b - a) ‚â§
      ‚àë k ‚àà Finset.range N, (stoppedValue f (upperCrossingTime a b f N (k + 1)) œâ -
        stoppedValue f (lowerCrossingTime a b f N k) œâ) := by
    calc
      ‚àë _k ‚àà Finset.range (upcrossingsBefore a b f N œâ), (b - a) ‚â§
          ‚àë k ‚àà Finset.range (upcrossingsBefore a b f N œâ),
            (stoppedValue f (upperCrossingTime a b f N (k + 1)) œâ -
              stoppedValue f (lowerCrossingTime a b f N k) œâ) := by
        refine Finset.sum_le_sum fun i hi =>
          le_sub_of_le_upcrossingsBefore (zero_lt_iff.2 hN) hab ?_
        rwa [Finset.mem_range] at hi
      _ ‚â§ ‚àë k ‚àà Finset.range N, (stoppedValue f (upperCrossingTime a b f N (k + 1)) œâ -
          stoppedValue f (lowerCrossingTime a b f N k) œâ) := by
        refine Finset.sum_le_sum_of_subset_of_nonneg
          (Finset.range_subset.2 (upcrossingsBefore_le f œâ hab)) fun i _ hi => ?_
        by_cases hi' : i = upcrossingsBefore a b f N œâ
        ¬∑ subst hi'
          simp only [stoppedValue]
          rw [upperCrossingTime_eq_of_upcrossingsBefore_lt hab (Nat.lt_succ_self _)]
          by_cases heq : lowerCrossingTime a b f N (upcrossingsBefore a b f N œâ) œâ = N
          ¬∑ rw [heq, sub_self]
          ¬∑ rw [sub_nonneg]
            exact le_trans (stoppedValue_lowerCrossingTime heq) hf
        ¬∑ rw [sub_eq_zero_of_upcrossingsBefore_lt hab]
          rw [Finset.mem_range, not_lt] at hi
          exact lt_of_le_of_ne hi (Ne.symm hi')
  refine le_trans ?_ h‚ÇÇ
  rw [Finset.sum_const, Finset.card_range, nsmul_eq_mul, mul_comm]
#align measure_theory.mul_upcrossings_before_le MeasureTheory.mul_upcrossingsBefore_le

theorem integral_mul_upcrossingsBefore_le_integral [IsFiniteMeasure Œº] (hf : Submartingale f ‚Ñ± Œº)
    (hfN : ‚àÄ œâ, a ‚â§ f N œâ) (hfzero : 0 ‚â§ f 0) (hab : a < b) :
    (b - a) * Œº[upcrossingsBefore a b f N] ‚â§ Œº[f N] :=
  calc
    (b - a) * Œº[upcrossingsBefore a b f N] ‚â§
        Œº[‚àë k ‚àà Finset.range N, upcrossingStrat a b f N k * (f (k + 1) - f k)] := by
      rw [‚Üê integral_mul_left]
      refine integral_mono_of_nonneg ?_ ((hf.sum_upcrossingStrat_mul a b N).integrable N) ?_
      ¬∑ exact eventually_of_forall fun œâ => mul_nonneg (sub_nonneg.2 hab.le) (Nat.cast_nonneg _)
      ¬∑ filter_upwards with œâ
        simpa using mul_upcrossingsBefore_le (hfN œâ) hab
    _ ‚â§ Œº[f N] - Œº[f 0] := hf.sum_mul_upcrossingStrat_le
    _ ‚â§ Œº[f N] := (sub_le_self_iff _).2 (integral_nonneg hfzero)
#align measure_theory.integral_mul_upcrossings_before_le_integral MeasureTheory.integral_mul_upcrossingsBefore_le_integral

theorem crossing_pos_eq (hab : a < b) :
    upperCrossingTime 0 (b - a) (fun n œâ => (f n œâ - a)‚Å∫) N n = upperCrossingTime a b f N n ‚àß
      lowerCrossingTime 0 (b - a) (fun n œâ => (f n œâ - a)‚Å∫) N n = lowerCrossingTime a b f N n := by
  have hab' : 0 < b - a := sub_pos.2 hab
  have hf : ‚àÄ œâ i, b - a ‚â§ (f i œâ - a)‚Å∫ ‚Üî b ‚â§ f i œâ := by
    intro i œâ
    refine ‚ü®fun h => ?_, fun h => ?_‚ü©
    ¬∑ rwa [‚Üê sub_le_sub_iff_right a, ‚Üê
        posPart_eq_of_posPart_pos (lt_of_lt_of_le hab' h)]
    ¬∑ rw [‚Üê sub_le_sub_iff_right a] at h
      rwa [posPart_eq_self.2 (le_trans hab'.le h)]
  have hf' (œâ i) : (f i œâ - a)‚Å∫ ‚â§ 0 ‚Üî f i œâ ‚â§ a := by rw [posPart_nonpos, sub_nonpos]
  induction' n with k ih
  ¬∑ refine ‚ü®rfl, ?_‚ü©
    #adaptation_note /-- nightly-2024-03-16: simp was
    simp (config := { unfoldPartialApp := true }) only [lowerCrossingTime_zero, hitting,
      Set.mem_Icc, Set.mem_Iic, Nat.zero_eq] -/
    simp (config := { unfoldPartialApp := true }) only [lowerCrossingTime_zero, hitting_def,
      Set.mem_Icc, Set.mem_Iic, Nat.zero_eq]
    ext œâ
    split_ifs with h‚ÇÅ h‚ÇÇ h‚ÇÇ
    ¬∑ simp_rw [hf']
    ¬∑ simp_rw [Set.mem_Iic, ‚Üê hf' _ _] at h‚ÇÇ
      exact False.elim (h‚ÇÇ h‚ÇÅ)
    ¬∑ simp_rw [Set.mem_Iic, hf' _ _] at h‚ÇÅ
      exact False.elim (h‚ÇÅ h‚ÇÇ)
    ¬∑ rfl
  ¬∑ have : upperCrossingTime 0 (b - a) (fun n œâ => (f n œâ - a)‚Å∫) N (k + 1) =
        upperCrossingTime a b f N (k + 1) := by
      ext œâ
      simp only [upperCrossingTime_succ_eq, ‚Üê ih.2, hitting, Set.mem_Ici, tsub_le_iff_right]
      split_ifs with h‚ÇÅ h‚ÇÇ h‚ÇÇ
      ¬∑ simp_rw [‚Üê sub_le_iff_le_add, hf œâ]
      ¬∑ refine False.elim (h‚ÇÇ ?_)
        simp_all only [Set.mem_Ici, not_true_eq_false]
      ¬∑ refine False.elim (h‚ÇÅ ?_)
        simp_all only [Set.mem_Ici]
      ¬∑ rfl
    refine ‚ü®this, ?_‚ü©
    ext œâ
    simp only [lowerCrossingTime, this, hitting, Set.mem_Iic]
    split_ifs with h‚ÇÅ h‚ÇÇ h‚ÇÇ
    ¬∑ simp_rw [hf' œâ]
    ¬∑ refine False.elim (h‚ÇÇ ?_)
      simp_all only [Set.mem_Iic, not_true_eq_false]
    ¬∑ refine False.elim (h‚ÇÅ ?_)
      simp_all only [Set.mem_Iic]
    ¬∑ rfl
#align measure_theory.crossing_pos_eq MeasureTheory.crossing_pos_eq

theorem upcrossingsBefore_pos_eq (hab : a < b) :
    upcrossingsBefore 0 (b - a) (fun n œâ => (f n œâ - a)‚Å∫) N œâ = upcrossingsBefore a b f N œâ := by
  simp_rw [upcrossingsBefore, (crossing_pos_eq hab).1]
#align measure_theory.upcrossings_before_pos_eq MeasureTheory.upcrossingsBefore_pos_eq

theorem mul_integral_upcrossingsBefore_le_integral_pos_part_aux [IsFiniteMeasure Œº]
    (hf : Submartingale f ‚Ñ± Œº) (hab : a < b) :
    (b - a) * Œº[upcrossingsBefore a b f N] ‚â§ Œº[fun œâ => (f N œâ - a)‚Å∫] := by
  refine le_trans (le_of_eq ?_)
    (integral_mul_upcrossingsBefore_le_integral (hf.sub_martingale (martingale_const _ _ _)).pos
      (fun œâ => posPart_nonneg _)
      (fun œâ => posPart_nonneg _) (sub_pos.2 hab))
  simp_rw [sub_zero, ‚Üê upcrossingsBefore_pos_eq hab]
  rfl
#align measure_theory.mul_integral_upcrossings_before_le_integral_pos_part_aux MeasureTheory.mul_integral_upcrossingsBefore_le_integral_pos_part_aux

/-- **Doob's upcrossing estimate**: given a real valued discrete submartingale `f` and real
values `a` and `b`, we have `(b - a) * ùîº[upcrossingsBefore a b f N] ‚â§ ùîº[(f N - a)‚Å∫]` where
`upcrossingsBefore a b f N` is the number of times the process `f` crossed from below `a` to above
`b` before the time `N`. -/
theorem Submartingale.mul_integral_upcrossingsBefore_le_integral_pos_part [IsFiniteMeasure Œº]
    (a b : ‚Ñù) (hf : Submartingale f ‚Ñ± Œº) (N : ‚Ñï) :
    (b - a) * Œº[upcrossingsBefore a b f N] ‚â§ Œº[fun œâ => (f N œâ - a)‚Å∫] := by
  by_cases hab : a < b
  ¬∑ exact mul_integral_upcrossingsBefore_le_integral_pos_part_aux hf hab
  ¬∑ rw [not_lt, ‚Üê sub_nonpos] at hab
    exact le_trans (mul_nonpos_of_nonpos_of_nonneg hab (by positivity))
      (integral_nonneg fun œâ => posPart_nonneg _)
#align measure_theory.submartingale.mul_integral_upcrossings_before_le_integral_pos_part MeasureTheory.Submartingale.mul_integral_upcrossingsBefore_le_integral_pos_part

/-!

### Variant of the upcrossing estimate

Now, we would like to prove a variant of the upcrossing estimate obtained by taking the supremum
over $N$ of the original upcrossing estimate. Namely, we want the inequality
$$
  (b - a) \sup_N \mathbb{E}[U_N(a, b)] \le \sup_N \mathbb{E}[f_N].
$$
This inequality is central for the martingale convergence theorem as it provides a uniform bound
for the upcrossings.

We note that on top of taking the supremum on both sides of the inequality, we had also used
the monotone convergence theorem on the left hand side to take the supremum outside of the
integral. To do this, we need to make sure $U_N(a, b)$ is measurable and integrable. Integrability
is easy to check as $U_N(a, b) ‚â§ N$ and so it suffices to show measurability. Indeed, by
noting that
$$
  U_N(a, b) = \sum_{i = 1}^N \mathbf{1}_{\{U_N(a, b) < N\}}
$$
$U_N(a, b)$ is measurable as $\{U_N(a, b) < N\}$ is a measurable set since $U_N(a, b)$ is a
stopping time.

-/


theorem upcrossingsBefore_eq_sum (hab : a < b) : upcrossingsBefore a b f N œâ =
    ‚àë i ‚àà Finset.Ico 1 (N + 1), {n | upperCrossingTime a b f N n œâ < N}.indicator 1 i := by
  by_cases hN : N = 0
  ¬∑ simp [hN]
  rw [‚Üê Finset.sum_Ico_consecutive _ (Nat.succ_le_succ zero_le')
    (Nat.succ_le_succ (upcrossingsBefore_le f œâ hab))]
  have h‚ÇÅ : ‚àÄ k ‚àà Finset.Ico 1 (upcrossingsBefore a b f N œâ + 1),
      {n : ‚Ñï | upperCrossingTime a b f N n œâ < N}.indicator 1 k = 1 := by
    rintro k hk
    rw [Finset.mem_Ico] at hk
    rw [Set.indicator_of_mem]
    ¬∑ rfl
    ¬∑ exact upperCrossingTime_lt_of_le_upcrossingsBefore (zero_lt_iff.2 hN) hab
        (Nat.lt_succ_iff.1 hk.2)
  have h‚ÇÇ : ‚àÄ k ‚àà Finset.Ico (upcrossingsBefore a b f N œâ + 1) (N + 1),
      {n : ‚Ñï | upperCrossingTime a b f N n œâ < N}.indicator 1 k = 0 := by
    rintro k hk
    rw [Finset.mem_Ico, Nat.succ_le_iff] at hk
    rw [Set.indicator_of_not_mem]
    simp only [Set.mem_setOf_eq, not_lt]
    exact (upperCrossingTime_eq_of_upcrossingsBefore_lt hab hk.1).symm.le
  rw [Finset.sum_congr rfl h‚ÇÅ, Finset.sum_congr rfl h‚ÇÇ, Finset.sum_const, Finset.sum_const,
    smul_eq_mul, mul_one, smul_eq_mul, mul_zero, Nat.card_Ico, Nat.add_succ_sub_one,
    add_zero, add_zero]
#align measure_theory.upcrossings_before_eq_sum MeasureTheory.upcrossingsBefore_eq_sum

theorem Adapted.measurable_upcrossingsBefore (hf : Adapted ‚Ñ± f) (hab : a < b) :
    Measurable (upcrossingsBefore a b f N) := by
  have : upcrossingsBefore a b f N = fun œâ =>
      ‚àë i ‚àà Finset.Ico 1 (N + 1), {n | upperCrossingTime a b f N n œâ < N}.indicator 1 i := by
    ext œâ
    exact upcrossingsBefore_eq_sum hab
  rw [this]
  exact Finset.measurable_sum _ fun i _ => Measurable.indicator measurable_const <|
    ‚Ñ±.le N _ (hf.isStoppingTime_upperCrossingTime.measurableSet_lt_of_pred N)
#align measure_theory.adapted.measurable_upcrossings_before MeasureTheory.Adapted.measurable_upcrossingsBefore

theorem Adapted.integrable_upcrossingsBefore [IsFiniteMeasure Œº] (hf : Adapted ‚Ñ± f) (hab : a < b) :
    Integrable (fun œâ => (upcrossingsBefore a b f N œâ : ‚Ñù)) Œº :=
  haveI : ‚àÄ·µê œâ ‚àÇŒº, ‚Äñ(upcrossingsBefore a b f N œâ : ‚Ñù)‚Äñ ‚â§ N := by
    filter_upwards with œâ
    rw [Real.norm_eq_abs, Nat.abs_cast, Nat.cast_le]
    exact upcrossingsBefore_le _ _ hab
  ‚ü®Measurable.aestronglyMeasurable (measurable_from_top.comp (hf.measurable_upcrossingsBefore hab)),
    hasFiniteIntegral_of_bounded this‚ü©
#align measure_theory.adapted.integrable_upcrossings_before MeasureTheory.Adapted.integrable_upcrossingsBefore

/-- The number of upcrossings of a realization of a stochastic process (`upcrossings` takes value
in `‚Ñù‚â•0‚àû` and so is allowed to be `‚àû`). -/
noncomputable def upcrossings [Preorder Œπ] [OrderBot Œπ] [InfSet Œπ] (a b : ‚Ñù) (f : Œπ ‚Üí Œ© ‚Üí ‚Ñù)
    (œâ : Œ©) : ‚Ñù‚â•0‚àû :=
  ‚®Ü N, (upcrossingsBefore a b f N œâ : ‚Ñù‚â•0‚àû)
#align measure_theory.upcrossings MeasureTheory.upcrossings

theorem Adapted.measurable_upcrossings (hf : Adapted ‚Ñ± f) (hab : a < b) :
    Measurable (upcrossings a b f) :=
  measurable_iSup fun _ => measurable_from_top.comp (hf.measurable_upcrossingsBefore hab)
#align measure_theory.adapted.measurable_upcrossings MeasureTheory.Adapted.measurable_upcrossings

theorem upcrossings_lt_top_iff :
    upcrossings a b f œâ < ‚àû ‚Üî ‚àÉ k, ‚àÄ N, upcrossingsBefore a b f N œâ ‚â§ k := by
  have : upcrossings a b f œâ < ‚àû ‚Üî ‚àÉ k : ‚Ñù‚â•0, upcrossings a b f œâ ‚â§ k := by
    constructor
    ¬∑ intro h
      lift upcrossings a b f œâ to ‚Ñù‚â•0 using h.ne with r hr
      exact ‚ü®r, le_rfl‚ü©
    ¬∑ rintro ‚ü®k, hk‚ü©
      exact lt_of_le_of_lt hk ENNReal.coe_lt_top
  simp_rw [this, upcrossings, iSup_le_iff]
  constructor <;> rintro ‚ü®k, hk‚ü©
  ¬∑ obtain ‚ü®m, hm‚ü© := exists_nat_ge k
    refine ‚ü®m, fun N => Nat.cast_le.1 ((hk N).trans ?_)‚ü©
    rwa [‚Üê ENNReal.coe_natCast, ENNReal.coe_le_coe]
  ¬∑ refine ‚ü®k, fun N => ?_‚ü©
    simp only [ENNReal.coe_natCast, Nat.cast_le, hk N]
#align measure_theory.upcrossings_lt_top_iff MeasureTheory.upcrossings_lt_top_iff

/-- A variant of Doob's upcrossing estimate obtained by taking the supremum on both sides. -/
theorem Submartingale.mul_lintegral_upcrossings_le_lintegral_pos_part [IsFiniteMeasure Œº] (a b : ‚Ñù)
    (hf : Submartingale f ‚Ñ± Œº) : ENNReal.ofReal (b - a) * ‚à´‚Åª œâ, upcrossings a b f œâ ‚àÇŒº ‚â§
      ‚®Ü N, ‚à´‚Åª œâ, ENNReal.ofReal ((f N œâ - a)‚Å∫) ‚àÇŒº := by
  by_cases hab : a < b
  ¬∑ simp_rw [upcrossings]
    have : ‚àÄ N, ‚à´‚Åª œâ, ENNReal.ofReal ((f N œâ - a)‚Å∫) ‚àÇŒº = ENNReal.ofReal (‚à´ œâ, (f N œâ - a)‚Å∫ ‚àÇŒº) := by
      intro N
      rw [ofReal_integral_eq_lintegral_ofReal]
      ¬∑ exact (hf.sub_martingale (martingale_const _ _ _)).pos.integrable _
      ¬∑ exact eventually_of_forall fun œâ => posPart_nonneg _
    rw [lintegral_iSup']
    ¬∑ simp_rw [this, ENNReal.mul_iSup, iSup_le_iff]
      intro N
      rw [(by simp :
          ‚à´‚Åª œâ, upcrossingsBefore a b f N œâ ‚àÇŒº = ‚à´‚Åª œâ, ‚Üë(upcrossingsBefore a b f N œâ : ‚Ñù‚â•0) ‚àÇŒº),
        lintegral_coe_eq_integral, ‚Üê ENNReal.ofReal_mul (sub_pos.2 hab).le]
      ¬∑ simp_rw [NNReal.coe_natCast]
        exact (ENNReal.ofReal_le_ofReal
          (hf.mul_integral_upcrossingsBefore_le_integral_pos_part a b N)).trans
            (le_iSup (Œ± := ‚Ñù‚â•0‚àû) _ N)
      ¬∑ simp only [NNReal.coe_natCast, hf.adapted.integrable_upcrossingsBefore hab]
    ¬∑ exact fun n => measurable_from_top.comp_aemeasurable
        (hf.adapted.measurable_upcrossingsBefore hab).aemeasurable
    ¬∑ filter_upwards with œâ N M hNM
      rw [Nat.cast_le]
      exact upcrossingsBefore_mono hab hNM œâ
  ¬∑ rw [not_lt, ‚Üê sub_nonpos] at hab
    rw [ENNReal.ofReal_of_nonpos hab, zero_mul]
    exact zero_le _
#align measure_theory.submartingale.mul_lintegral_upcrossings_le_lintegral_pos_part MeasureTheory.Submartingale.mul_lintegral_upcrossings_le_lintegral_pos_part

end MeasureTheory
