/-
Copyright (c) 2022 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Probability.IdentDistrib
import Mathlib.Probability.Independence.Integrable
import Mathlib.MeasureTheory.Integral.DominatedConvergence
import Mathlib.Analysis.SpecificLimits.FloorPow
import Mathlib.Analysis.PSeries
import Mathlib.Analysis.Asymptotics.SpecificAsymptotics

/-!
# The strong law of large numbers

We prove the strong law of large numbers, in `ProbabilityTheory.strong_law_ae`:
If `X n` is a sequence of independent identically distributed integrable random
variables, then `âˆ‘ i âˆˆ range n, X i / n` converges almost surely to `ğ”¼[X 0]`.
We give here the strong version, due to Etemadi, that only requires pairwise independence.

This file also contains the Láµ– version of the strong law of large numbers provided by
`ProbabilityTheory.strong_law_Lp` which shows `âˆ‘ i âˆˆ range n, X i / n` converges in Láµ– to
`ğ”¼[X 0]` provided `X n` is independent identically distributed and is Láµ–.

## Implementation

The main point is to prove the result for real-valued random variables, as the general case
of Banach-space valued random variables follows from this case and approximation by simple
functions. The real version is given in `ProbabilityTheory.strong_law_ae_real`.

We follow the proof by Etemadi
[Etemadi, *An elementary proof of the strong law of large numbers*][etemadi_strong_law],
which goes as follows.

It suffices to prove the result for nonnegative `X`, as one can prove the general result by
splitting a general `X` into its positive part and negative part.
Consider `Xâ‚™` a sequence of nonnegative integrable identically distributed pairwise independent
random variables. Let `Yâ‚™` be the truncation of `Xâ‚™` up to `n`. We claim that
* Almost surely, `Xâ‚™ = Yâ‚™` for all but finitely many indices. Indeed, `âˆ‘ â„™ (Xâ‚™ â‰  Yâ‚™)` is bounded by
  `1 + ğ”¼[X]` (see `sum_prob_mem_Ioc_le` and `tsum_prob_mem_Ioi_lt_top`).
* Let `c > 1`. Along the sequence `n = c ^ k`, then `(âˆ‘_{i=0}^{n-1} Yáµ¢ - ğ”¼[Yáµ¢])/n` converges almost
  surely to `0`. This follows from a variance control, as
```
  âˆ‘_k â„™ (|âˆ‘_{i=0}^{c^k - 1} Yáµ¢ - ğ”¼[Yáµ¢]| > c^k Îµ)
    â‰¤ âˆ‘_k (c^k Îµ)^{-2} âˆ‘_{i=0}^{c^k - 1} Var[Yáµ¢]    (by Markov inequality)
    â‰¤ âˆ‘_i (C/i^2) Var[Yáµ¢]                           (as âˆ‘_{c^k > i} 1/(c^k)^2 â‰¤ C/i^2)
    â‰¤ âˆ‘_i (C/i^2) ğ”¼[Yáµ¢^2]
    â‰¤ 2C ğ”¼[X^2]                                     (see `sum_variance_truncation_le`)
```
* As `ğ”¼[Yáµ¢]` converges to `ğ”¼[X]`, it follows from the two previous items and CesÃ ro that, along
  the sequence `n = c^k`, one has `(âˆ‘_{i=0}^{n-1} Xáµ¢) / n â†’ ğ”¼[X]` almost surely.
* To generalize it to all indices, we use the fact that `âˆ‘_{i=0}^{n-1} Xáµ¢` is nondecreasing and
  that, if `c` is close enough to `1`, the gap between `c^k` and `c^(k+1)` is small.
-/


noncomputable section

open MeasureTheory Filter Finset Asymptotics

open Set (indicator)

open scoped Topology MeasureTheory ProbabilityTheory ENNReal NNReal

open scoped Function -- required for scoped `on` notation

namespace ProbabilityTheory

/-! ### Prerequisites on truncations -/


section Truncation

variable {Î± : Type*}

/-- Truncating a real-valued function to the interval `(-A, A]`. -/
def truncation (f : Î± â†’ â„) (A : â„) :=
  indicator (Set.Ioc (-A) A) id âˆ˜ f

variable {m : MeasurableSpace Î±} {Î¼ : Measure Î±} {f : Î± â†’ â„}

theorem _root_.MeasureTheory.AEStronglyMeasurable.truncation (hf : AEStronglyMeasurable f Î¼)
    {A : â„} : AEStronglyMeasurable (truncation f A) Î¼ := by
  apply AEStronglyMeasurable.comp_aemeasurable _ hf.aemeasurable
  exact (stronglyMeasurable_id.indicator measurableSet_Ioc).aestronglyMeasurable

theorem abs_truncation_le_bound (f : Î± â†’ â„) (A : â„) (x : Î±) : |truncation f A x| â‰¤ |A| := by
  simp only [truncation, Set.indicator, id, Function.comp_apply]
  split_ifs with h
  Â· exact abs_le_abs h.2 (neg_le.2 h.1.le)
  Â· simp [abs_nonneg]

@[simp]
theorem truncation_zero (f : Î± â†’ â„) : truncation f 0 = 0 := by simp [truncation]; rfl

theorem abs_truncation_le_abs_self (f : Î± â†’ â„) (A : â„) (x : Î±) : |truncation f A x| â‰¤ |f x| := by
  simp only [truncation, indicator, id, Function.comp_apply]
  split_ifs
  Â· exact le_rfl
  Â· simp [abs_nonneg]

theorem truncation_eq_self {f : Î± â†’ â„} {A : â„} {x : Î±} (h : |f x| < A) :
    truncation f A x = f x := by
  simp only [truncation, indicator, id, Function.comp_apply, ite_eq_left_iff]
  intro H
  apply H.elim
  simp [(abs_lt.1 h).1, (abs_lt.1 h).2.le]

theorem truncation_eq_of_nonneg {f : Î± â†’ â„} {A : â„} (h : âˆ€ x, 0 â‰¤ f x) :
    truncation f A = indicator (Set.Ioc 0 A) id âˆ˜ f := by
  ext x
  rcases (h x).lt_or_eq with (hx | hx)
  Â· simp only [truncation, indicator, hx, Set.mem_Ioc, id, Function.comp_apply]
    by_cases h'x : f x â‰¤ A
    Â· have : -A < f x := by linarith [h x]
      simp only [this, true_and]
    Â· simp only [h'x, and_false]
  Â· simp only [truncation, indicator, hx, id, Function.comp_apply, ite_self]

theorem truncation_nonneg {f : Î± â†’ â„} (A : â„) {x : Î±} (h : 0 â‰¤ f x) : 0 â‰¤ truncation f A x :=
  Set.indicator_apply_nonneg fun _ => h

theorem _root_.MeasureTheory.AEStronglyMeasurable.memLp_truncation [IsFiniteMeasure Î¼]
    (hf : AEStronglyMeasurable f Î¼) {A : â„} {p : â„â‰¥0âˆ} : MemLp (truncation f A) p Î¼ :=
  MemLp.of_bound hf.truncation |A| (Eventually.of_forall fun _ => abs_truncation_le_bound _ _ _)

theorem _root_.MeasureTheory.AEStronglyMeasurable.integrable_truncation [IsFiniteMeasure Î¼]
    (hf : AEStronglyMeasurable f Î¼) {A : â„} : Integrable (truncation f A) Î¼ := by
  rw [â† memLp_one_iff_integrable]; exact hf.memLp_truncation

theorem moment_truncation_eq_intervalIntegral (hf : AEStronglyMeasurable f Î¼) {A : â„} (hA : 0 â‰¤ A)
    {n : â„•} (hn : n â‰  0) : âˆ« x, truncation f A x ^ n âˆ‚Î¼ = âˆ« y in -A..A, y ^ n âˆ‚Measure.map f Î¼ := by
  have M : MeasurableSet (Set.Ioc (-A) A) := measurableSet_Ioc
  change âˆ« x, (fun z => indicator (Set.Ioc (-A) A) id z ^ n) (f x) âˆ‚Î¼ = _
  rw [â† integral_map (f := fun z => _ ^ n) hf.aemeasurable, intervalIntegral.integral_of_le,
    â† integral_indicator M]
  Â· simp only [indicator, zero_pow hn, id, ite_pow]
  Â· linarith
  Â· exact ((measurable_id.indicator M).pow_const n).aestronglyMeasurable

theorem moment_truncation_eq_intervalIntegral_of_nonneg (hf : AEStronglyMeasurable f Î¼) {A : â„}
    {n : â„•} (hn : n â‰  0) (h'f : 0 â‰¤ f) :
    âˆ« x, truncation f A x ^ n âˆ‚Î¼ = âˆ« y in 0..A, y ^ n âˆ‚Measure.map f Î¼ := by
  have M : MeasurableSet (Set.Ioc 0 A) := measurableSet_Ioc
  have M' : MeasurableSet (Set.Ioc A 0) := measurableSet_Ioc
  rw [truncation_eq_of_nonneg h'f]
  change âˆ« x, (fun z => indicator (Set.Ioc 0 A) id z ^ n) (f x) âˆ‚Î¼ = _
  rcases le_or_gt 0 A with (hA | hA)
  Â· rw [â† integral_map (f := fun z => _ ^ n) hf.aemeasurable, intervalIntegral.integral_of_le hA,
      â† integral_indicator M]
    Â· simp only [indicator, zero_pow hn, id, ite_pow]
    Â· exact ((measurable_id.indicator M).pow_const n).aestronglyMeasurable
  Â· rw [â† integral_map (f := fun z => _ ^ n) hf.aemeasurable, intervalIntegral.integral_of_ge hA.le,
      â† integral_indicator M']
    Â· simp only [Set.Ioc_eq_empty_of_le hA.le, zero_pow hn, Set.indicator_empty, integral_zero,
        zero_eq_neg]
      apply integral_eq_zero_of_ae
      have : âˆ€áµ x âˆ‚Measure.map f Î¼, (0 : â„) â‰¤ x :=
        (ae_map_iff hf.aemeasurable measurableSet_Ici).2 (Eventually.of_forall h'f)
      filter_upwards [this] with x hx
      simp only [indicator, Set.mem_Ioc, Pi.zero_apply, ite_eq_right_iff, and_imp]
      intro _ h''x
      have : x = 0 := by linarith
      simp [this, zero_pow hn]
    Â· exact ((measurable_id.indicator M).pow_const n).aestronglyMeasurable

theorem integral_truncation_eq_intervalIntegral (hf : AEStronglyMeasurable f Î¼) {A : â„}
    (hA : 0 â‰¤ A) : âˆ« x, truncation f A x âˆ‚Î¼ = âˆ« y in -A..A, y âˆ‚Measure.map f Î¼ := by
  simpa using moment_truncation_eq_intervalIntegral hf hA one_ne_zero

theorem integral_truncation_eq_intervalIntegral_of_nonneg (hf : AEStronglyMeasurable f Î¼) {A : â„}
    (h'f : 0 â‰¤ f) : âˆ« x, truncation f A x âˆ‚Î¼ = âˆ« y in 0..A, y âˆ‚Measure.map f Î¼ := by
  simpa using moment_truncation_eq_intervalIntegral_of_nonneg hf one_ne_zero h'f

theorem integral_truncation_le_integral_of_nonneg (hf : Integrable f Î¼) (h'f : 0 â‰¤ f) {A : â„} :
    âˆ« x, truncation f A x âˆ‚Î¼ â‰¤ âˆ« x, f x âˆ‚Î¼ := by
  apply integral_mono_of_nonneg
    (Eventually.of_forall fun x => ?_) hf (Eventually.of_forall fun x => ?_)
  Â· exact truncation_nonneg _ (h'f x)
  Â· calc
      truncation f A x â‰¤ |truncation f A x| := le_abs_self _
      _ â‰¤ |f x| := abs_truncation_le_abs_self _ _ _
      _ = f x := abs_of_nonneg (h'f x)

/-- If a function is integrable, then the integral of its truncated versions converges to the
integral of the whole function. -/
theorem tendsto_integral_truncation {f : Î± â†’ â„} (hf : Integrable f Î¼) :
    Tendsto (fun A => âˆ« x, truncation f A x âˆ‚Î¼) atTop (ğ“ (âˆ« x, f x âˆ‚Î¼)) := by
  refine tendsto_integral_filter_of_dominated_convergence (fun x => abs (f x)) ?_ ?_ ?_ ?_
  Â· exact Eventually.of_forall fun A â†¦ hf.aestronglyMeasurable.truncation
  Â· filter_upwards with A
    filter_upwards with x
    rw [Real.norm_eq_abs]
    exact abs_truncation_le_abs_self _ _ _
  Â· exact hf.abs
  Â· filter_upwards with x
    apply tendsto_const_nhds.congr' _
    filter_upwards [Ioi_mem_atTop (abs (f x))] with A hA
    exact (truncation_eq_self hA).symm

theorem IdentDistrib.truncation {Î² : Type*} [MeasurableSpace Î²] {Î½ : Measure Î²} {f : Î± â†’ â„}
    {g : Î² â†’ â„} (h : IdentDistrib f g Î¼ Î½) {A : â„} :
    IdentDistrib (truncation f A) (truncation g A) Î¼ Î½ :=
  h.comp (measurable_id.indicator measurableSet_Ioc)

end Truncation

section StrongLawAeReal

variable {Î© : Type*} [MeasureSpace Î©] [IsProbabilityMeasure (â„™ : Measure Î©)]

section MomentEstimates

theorem sum_prob_mem_Ioc_le {X : Î© â†’ â„} (hint : Integrable X) (hnonneg : 0 â‰¤ X) {K : â„•} {N : â„•}
    (hKN : K â‰¤ N) :
    âˆ‘ j âˆˆ range K, â„™ {Ï‰ | X Ï‰ âˆˆ Set.Ioc (j : â„) N} â‰¤ ENNReal.ofReal (ğ”¼[X] + 1) := by
  let Ï : Measure â„ := Measure.map X â„™
  haveI : IsProbabilityMeasure Ï := isProbabilityMeasure_map hint.aemeasurable
  have A : âˆ‘ j âˆˆ range K, âˆ« _ in j..N, (1 : â„) âˆ‚Ï â‰¤ ğ”¼[X] + 1 :=
    calc
      âˆ‘ j âˆˆ range K, âˆ« _ in j..N, (1 : â„) âˆ‚Ï =
          âˆ‘ j âˆˆ range K, âˆ‘ i âˆˆ Ico j N, âˆ« _ in i..(i + 1 : â„•), (1 : â„) âˆ‚Ï := by
        apply sum_congr rfl fun j hj => ?_
        rw [intervalIntegral.sum_integral_adjacent_intervals_Ico ((mem_range.1 hj).le.trans hKN)]
        intro k _
        exact continuous_const.intervalIntegrable _ _
      _ = âˆ‘ i âˆˆ range N, âˆ‘ j âˆˆ range (min (i + 1) K), âˆ« _ in i..(i + 1 : â„•), (1 : â„) âˆ‚Ï := by
        simp_rw [sum_sigma']
        refine sum_nbij' (fun p â†¦ âŸ¨p.2, p.1âŸ©) (fun p â†¦ âŸ¨p.2, p.1âŸ©) ?_ ?_ ?_ ?_ ?_ <;>
          aesop (add simp Nat.lt_succ_iff)
      _ â‰¤ âˆ‘ i âˆˆ range N, (i + 1) * âˆ« _ in i..(i + 1 : â„•), (1 : â„) âˆ‚Ï := by
        gcongr with i
        simp only [Nat.cast_add, Nat.cast_one, sum_const, card_range, nsmul_eq_mul, Nat.cast_min]
        refine mul_le_mul_of_nonneg_right (min_le_left _ _) ?_
        apply intervalIntegral.integral_nonneg
        Â· simp only [le_add_iff_nonneg_right, zero_le_one]
        Â· simp only [zero_le_one, imp_true_iff]
      _ â‰¤ âˆ‘ i âˆˆ range N, âˆ« x in i..(i + 1 : â„•), x + 1 âˆ‚Ï := by
        gcongr with i
        have I : (i : â„) â‰¤ (i + 1 : â„•) := by
          simp only [Nat.cast_add, Nat.cast_one, le_add_iff_nonneg_right, zero_le_one]
        simp_rw [intervalIntegral.integral_of_le I, â† integral_const_mul]
        apply setIntegral_mono_on
        Â· exact continuous_const.integrableOn_Ioc
        Â· exact (continuous_id.add continuous_const).integrableOn_Ioc
        Â· exact measurableSet_Ioc
        Â· intro x hx
          simp only [Nat.cast_add, Nat.cast_one, Set.mem_Ioc] at hx
          simp [hx.1.le]
      _ = âˆ« x in 0..N, x + 1 âˆ‚Ï := by
        rw [intervalIntegral.sum_integral_adjacent_intervals fun k _ => ?_]
        Â· norm_cast
        Â· exact (continuous_id.add continuous_const).intervalIntegrable _ _
      _ = âˆ« x in 0..N, x âˆ‚Ï + âˆ« x in 0..N, 1 âˆ‚Ï := by
        rw [intervalIntegral.integral_add]
        Â· exact continuous_id.intervalIntegrable _ _
        Â· exact continuous_const.intervalIntegrable _ _
      _ = ğ”¼[truncation X N] + âˆ« x in 0..N, 1 âˆ‚Ï := by
        rw [integral_truncation_eq_intervalIntegral_of_nonneg hint.1 hnonneg]
      _ â‰¤ ğ”¼[X] + âˆ« x in 0..N, 1 âˆ‚Ï :=
        (add_le_add_right (integral_truncation_le_integral_of_nonneg hint hnonneg) _)
      _ â‰¤ ğ”¼[X] + 1 := by
        refine add_le_add le_rfl ?_
        rw [intervalIntegral.integral_of_le (Nat.cast_nonneg _)]
        simp only [integral_const, measureReal_restrict_apply', measurableSet_Ioc, Set.univ_inter,
          Algebra.id.smul_eq_mul, mul_one]
        rw [â† ENNReal.toReal_one]
        exact ENNReal.toReal_mono ENNReal.one_ne_top prob_le_one
  have B : âˆ€ a b, â„™ {Ï‰ | X Ï‰ âˆˆ Set.Ioc a b} = ENNReal.ofReal (âˆ« _ in Set.Ioc a b, (1 : â„) âˆ‚Ï) := by
    intro a b
    rw [ofReal_setIntegral_one Ï _,
      Measure.map_apply_of_aemeasurable hint.aemeasurable measurableSet_Ioc]
    rfl
  calc
    âˆ‘ j âˆˆ range K, â„™ {Ï‰ | X Ï‰ âˆˆ Set.Ioc (j : â„) N} =
        âˆ‘ j âˆˆ range K, ENNReal.ofReal (âˆ« _ in Set.Ioc (j : â„) N, (1 : â„) âˆ‚Ï) := by simp_rw [B]
    _ = ENNReal.ofReal (âˆ‘ j âˆˆ range K, âˆ« _ in Set.Ioc (j : â„) N, (1 : â„) âˆ‚Ï) := by
      simp [ENNReal.ofReal_sum_of_nonneg]
    _ = ENNReal.ofReal (âˆ‘ j âˆˆ range K, âˆ« _ in (j : â„)..N, (1 : â„) âˆ‚Ï) := by
      congr 1
      refine sum_congr rfl fun j hj => ?_
      rw [intervalIntegral.integral_of_le (Nat.cast_le.2 ((mem_range.1 hj).le.trans hKN))]
    _ â‰¤ ENNReal.ofReal (ğ”¼[X] + 1) := ENNReal.ofReal_le_ofReal A

theorem tsum_prob_mem_Ioi_lt_top {X : Î© â†’ â„} (hint : Integrable X) (hnonneg : 0 â‰¤ X) :
    (âˆ‘' j : â„•, â„™ {Ï‰ | X Ï‰ âˆˆ Set.Ioi (j : â„)}) < âˆ := by
  suffices âˆ€ K : â„•, âˆ‘ j âˆˆ range K, â„™ {Ï‰ | X Ï‰ âˆˆ Set.Ioi (j : â„)} â‰¤ ENNReal.ofReal (ğ”¼[X] + 1) from
    (le_of_tendsto_of_tendsto (ENNReal.tendsto_nat_tsum _) tendsto_const_nhds
      (Eventually.of_forall this)).trans_lt ENNReal.ofReal_lt_top
  intro K
  have A : Tendsto (fun N : â„• => âˆ‘ j âˆˆ range K, â„™ {Ï‰ | X Ï‰ âˆˆ Set.Ioc (j : â„) N}) atTop
      (ğ“ (âˆ‘ j âˆˆ range K, â„™ {Ï‰ | X Ï‰ âˆˆ Set.Ioi (j : â„)})) := by
    refine tendsto_finset_sum _ fun i _ => ?_
    have : {Ï‰ | X Ï‰ âˆˆ Set.Ioi (i : â„)} = â‹ƒ N : â„•, {Ï‰ | X Ï‰ âˆˆ Set.Ioc (i : â„) N} := by
      apply Set.Subset.antisymm _ _
      Â· intro Ï‰ hÏ‰
        obtain âŸ¨N, hNâŸ© : âˆƒ N : â„•, X Ï‰ â‰¤ N := exists_nat_ge (X Ï‰)
        exact Set.mem_iUnion.2 âŸ¨N, hÏ‰, hNâŸ©
      Â· simp +contextual only [Set.mem_Ioc, Set.mem_Ioi,
          Set.iUnion_subset_iff, Set.setOf_subset_setOf, imp_true_iff]
    rw [this]
    apply tendsto_measure_iUnion_atTop
    intro m n hmn x hx
    exact âŸ¨hx.1, hx.2.trans (Nat.cast_le.2 hmn)âŸ©
  apply le_of_tendsto_of_tendsto A tendsto_const_nhds
  filter_upwards [Ici_mem_atTop K] with N hN
  exact sum_prob_mem_Ioc_le hint hnonneg hN

theorem sum_variance_truncation_le {X : Î© â†’ â„} (hint : Integrable X) (hnonneg : 0 â‰¤ X) (K : â„•) :
    âˆ‘ j âˆˆ range K, ((j : â„) ^ 2)â»Â¹ * ğ”¼[truncation X j ^ 2] â‰¤ 2 * ğ”¼[X] := by
  set Y := fun n : â„• => truncation X n
  let Ï : Measure â„ := Measure.map X â„™
  have Y2 : âˆ€ n, ğ”¼[Y n ^ 2] = âˆ« x in 0..n, x ^ 2 âˆ‚Ï := by
    intro n
    change ğ”¼[fun x => Y n x ^ 2] = _
    rw [moment_truncation_eq_intervalIntegral_of_nonneg hint.1 two_ne_zero hnonneg]
  calc
    âˆ‘ j âˆˆ range K, ((j : â„) ^ 2)â»Â¹ * ğ”¼[Y j ^ 2] =
        âˆ‘ j âˆˆ range K, ((j : â„) ^ 2)â»Â¹ * âˆ« x in 0..j, x ^ 2 âˆ‚Ï := by simp_rw [Y2]
    _ = âˆ‘ j âˆˆ range K, ((j : â„) ^ 2)â»Â¹ * âˆ‘ k âˆˆ range j, âˆ« x in k..(k + 1 : â„•), x ^ 2 âˆ‚Ï := by
      congr 1 with j
      congr 1
      rw [intervalIntegral.sum_integral_adjacent_intervals]
      Â· norm_cast
      intro k _
      exact (continuous_id.pow _).intervalIntegrable _ _
    _ = âˆ‘ k âˆˆ range K, (âˆ‘ j âˆˆ Ioo k K, ((j : â„) ^ 2)â»Â¹) * âˆ« x in k..(k + 1 : â„•), x ^ 2 âˆ‚Ï := by
      simp_rw [mul_sum, sum_mul, sum_sigma']
      refine sum_nbij' (fun p â†¦ âŸ¨p.2, p.1âŸ©) (fun p â†¦ âŸ¨p.2, p.1âŸ©) ?_ ?_ ?_ ?_ ?_ <;>
        aesop (add unsafe lt_trans)
    _ â‰¤ âˆ‘ k âˆˆ range K, 2 / (k + 1 : â„) * âˆ« x in k..(k + 1 : â„•), x ^ 2 âˆ‚Ï := by
      gcongr with k
      Â· refine intervalIntegral.integral_nonneg_of_forall ?_ fun u => sq_nonneg _
        simp only [Nat.cast_add, Nat.cast_one, le_add_iff_nonneg_right, zero_le_one]
      Â· apply sum_Ioo_inv_sq_le
    _ â‰¤ âˆ‘ k âˆˆ range K, âˆ« x in k..(k + 1 : â„•), 2 * x âˆ‚Ï := by
      gcongr with k
      have Ik : (k : â„) â‰¤ (k + 1 : â„•) := by simp
      rw [â† intervalIntegral.integral_const_mul, intervalIntegral.integral_of_le Ik,
        intervalIntegral.integral_of_le Ik]
      refine setIntegral_mono_on ?_ ?_ measurableSet_Ioc fun x hx => ?_
      Â· apply Continuous.integrableOn_Ioc
        exact continuous_const.mul (continuous_pow 2)
      Â· apply Continuous.integrableOn_Ioc
        exact continuous_const.mul continuous_id'
      Â· calc
          2 / (â†‘k + 1) * x ^ 2 = x / (k + 1) * (2 * x) := by ring
          _ â‰¤ 1 * (2 * x) := by
              have : 0 < x := k.cast_nonneg.trans_lt hx.1
              gcongr
              exact (div_le_one <| by positivity).2 <| mod_cast hx.2
          _ = 2 * x := by rw [one_mul]
    _ = 2 * âˆ« x in (0 : â„)..K, x âˆ‚Ï := by
      rw [intervalIntegral.sum_integral_adjacent_intervals fun k _ => ?_]
      swap; Â· exact (continuous_const.mul continuous_id').intervalIntegrable _ _
      rw [intervalIntegral.integral_const_mul]
      norm_cast
    _ â‰¤ 2 * ğ”¼[X] := mul_le_mul_of_nonneg_left (by
      rw [â† integral_truncation_eq_intervalIntegral_of_nonneg hint.1 hnonneg]
      exact integral_truncation_le_integral_of_nonneg hint hnonneg) zero_le_two

end MomentEstimates

/-! Proof of the strong law of large numbers (almost sure version, assuming only
pairwise independence) for nonnegative random variables, following Etemadi's proof. -/
section StrongLawNonneg

variable (X : â„• â†’ Î© â†’ â„) (hint : Integrable (X 0))
  (hindep : Pairwise (IndepFun on X)) (hident : âˆ€ i, IdentDistrib (X i) (X 0))
  (hnonneg : âˆ€ i Ï‰, 0 â‰¤ X i Ï‰)

include hint hindep hident hnonneg in
/-- The truncation of `Xáµ¢` up to `i` satisfies the strong law of large numbers (with respect to
the truncated expectation) along the sequence `c^n`, for any `c > 1`, up to a given `Îµ > 0`.
This follows from a variance control. -/
theorem strong_law_aux1 {c : â„} (c_one : 1 < c) {Îµ : â„} (Îµpos : 0 < Îµ) : âˆ€áµ Ï‰, âˆ€á¶  n : â„• in atTop,
    |âˆ‘ i âˆˆ range âŒŠc ^ nâŒ‹â‚Š, truncation (X i) i Ï‰ - ğ”¼[âˆ‘ i âˆˆ range âŒŠc ^ nâŒ‹â‚Š, truncation (X i) i]| <
    Îµ * âŒŠc ^ nâŒ‹â‚Š := by
  /- Let `S n = âˆ‘ i âˆˆ range n, Y i` where `Y i = truncation (X i) i`. We should show that
    `|S k - ğ”¼[S k]| / k â‰¤ Îµ` along the sequence of powers of `c`. For this, we apply Borel-Cantelli:
    it suffices to show that the converse probabilities are summable. From Chebyshev inequality,
    this will follow from a variance control `âˆ‘' Var[S (c^i)] / (c^i)^2 < âˆ`. This is checked in
    `I2` using pairwise independence to expand the variance of the sum as the sum of the variances,
    and then a straightforward but tedious computation (essentially boiling down to the fact that
    the sum of `1/(c ^ i)^2` beyond a threshold `j` is comparable to `1/j^2`).
    Note that we have written `c^i` in the above proof sketch, but rigorously one should put integer
    parts everywhere, making things more painful. We write `u i = âŒŠc^iâŒ‹â‚Š` for brevity. -/
  have c_pos : 0 < c := zero_lt_one.trans c_one
  have hX : âˆ€ i, AEStronglyMeasurable (X i) â„™ := fun i =>
    (hident i).symm.aestronglyMeasurable_snd hint.1
  have A : âˆ€ i, StronglyMeasurable (indicator (Set.Ioc (-i : â„) i) id) := fun i =>
    stronglyMeasurable_id.indicator measurableSet_Ioc
  set Y := fun n : â„• => truncation (X n) n
  set S := fun n => âˆ‘ i âˆˆ range n, Y i with hS
  let u : â„• â†’ â„• := fun n => âŒŠc ^ nâŒ‹â‚Š
  have u_mono : Monotone u := fun i j hij => Nat.floor_mono (pow_right_monoâ‚€ c_one.le hij)
  have I1 : âˆ€ K, âˆ‘ j âˆˆ range K, ((j : â„) ^ 2)â»Â¹ * Var[Y j] â‰¤ 2 * ğ”¼[X 0] := by
    intro K
    calc
      âˆ‘ j âˆˆ range K, ((j : â„) ^ 2)â»Â¹ * Var[Y j] â‰¤
          âˆ‘ j âˆˆ range K, ((j : â„) ^ 2)â»Â¹ * ğ”¼[truncation (X 0) j ^ 2] := by
        gcongr with j
        rw [(hident j).truncation.variance_eq]
        exact variance_le_expectation_sq (hX 0).truncation
      _ â‰¤ 2 * ğ”¼[X 0] := sum_variance_truncation_le hint (hnonneg 0) K
  let C := c ^ 5 * (c - 1)â»Â¹ ^ 3 * (2 * ğ”¼[X 0])
  have I2 : âˆ€ N, âˆ‘ i âˆˆ range N, ((u i : â„) ^ 2)â»Â¹ * Var[S (u i)] â‰¤ C := by
    intro N
    calc
      âˆ‘ i âˆˆ range N, ((u i : â„) ^ 2)â»Â¹ * Var[S (u i)] =
          âˆ‘ i âˆˆ range N, ((u i : â„) ^ 2)â»Â¹ * âˆ‘ j âˆˆ range (u i), Var[Y j] := by
        congr 1 with i
        congr 1
        rw [hS, IndepFun.variance_sum]
        Â· intro j _
          exact (hident j).aestronglyMeasurable_fst.memLp_truncation
        Â· intro k _ l _ hkl
          exact (hindep hkl).comp (A k).measurable (A l).measurable
      _ = âˆ‘ j âˆˆ range (u (N - 1)), (âˆ‘ i âˆˆ range N with j < u i, ((u i : â„) ^ 2)â»Â¹) * Var[Y j] := by
        simp_rw [mul_sum, sum_mul, sum_sigma']
        refine sum_nbij' (fun p â†¦ âŸ¨p.2, p.1âŸ©) (fun p â†¦ âŸ¨p.2, p.1âŸ©) ?_ ?_ ?_ ?_ ?_
        Â· simp only [mem_sigma, mem_range, mem_filter, and_imp,
            Sigma.forall]
          exact fun a b haN hb â†¦ âŸ¨hb.trans_le <| u_mono <| Nat.le_pred_of_lt haN, haN, hbâŸ©
        all_goals simp
      _ â‰¤ âˆ‘ j âˆˆ range (u (N - 1)), c ^ 5 * (c - 1)â»Â¹ ^ 3 / â†‘j ^ 2 * Var[Y j] := by
        gcongr âˆ‘ _ âˆˆ _, ?_ with j
        rcases eq_zero_or_pos j with (rfl | hj)
        Â· simp only [Nat.cast_zero]
          simp only [Y, Nat.cast_zero, truncation_zero, variance_zero, mul_zero, le_rfl]
        apply mul_le_mul_of_nonneg_right _ (variance_nonneg _ _)
        convert sum_div_nat_floor_pow_sq_le_div_sq N (Nat.cast_pos.2 hj) c_one using 2
        Â· simp only [u, Nat.cast_lt]
        Â· simp only [u, one_div]
      _ = c ^ 5 * (c - 1)â»Â¹ ^ 3 * âˆ‘ j âˆˆ range (u (N - 1)), ((j : â„) ^ 2)â»Â¹ * Var[Y j] := by
        simp_rw [mul_sum, div_eq_mul_inv, mul_assoc]
      _ â‰¤ c ^ 5 * (c - 1)â»Â¹ ^ 3 * (2 * ğ”¼[X 0]) := by
        apply mul_le_mul_of_nonneg_left (I1 _)
        apply mul_nonneg (pow_nonneg c_pos.le _)
        exact pow_nonneg (inv_nonneg.2 (sub_nonneg.2 c_one.le)) _
  have I3 : âˆ€ N, âˆ‘ i âˆˆ range N, â„™ {Ï‰ | (u i * Îµ : â„) â‰¤ |S (u i) Ï‰ - ğ”¼[S (u i)]|} â‰¤
      ENNReal.ofReal (Îµâ»Â¹ ^ 2 * C) := by
    intro N
    calc
      âˆ‘ i âˆˆ range N, â„™ {Ï‰ | (u i * Îµ : â„) â‰¤ |S (u i) Ï‰ - ğ”¼[S (u i)]|} â‰¤
          âˆ‘ i âˆˆ range N, ENNReal.ofReal (Var[S (u i)] / (u i * Îµ) ^ 2) := by
        refine sum_le_sum fun i _ => ?_
        apply meas_ge_le_variance_div_sq
        Â· exact memLp_finset_sum' _ fun j _ => (hident j).aestronglyMeasurable_fst.memLp_truncation
        Â· apply mul_pos (Nat.cast_pos.2 _) Îµpos
          refine zero_lt_one.trans_le ?_
          apply Nat.le_floor
          rw [Nat.cast_one]
          apply one_le_powâ‚€ c_one.le
      _ = ENNReal.ofReal (âˆ‘ i âˆˆ range N, Var[S (u i)] / (u i * Îµ) ^ 2) := by
        rw [ENNReal.ofReal_sum_of_nonneg fun i _ => ?_]
        exact div_nonneg (variance_nonneg _ _) (sq_nonneg _)
      _ â‰¤ ENNReal.ofReal (Îµâ»Â¹ ^ 2 * C) := by
        apply ENNReal.ofReal_le_ofReal
        simp_rw [div_eq_inv_mul, â† inv_pow, mul_inv, mul_comm _ (Îµâ»Â¹), mul_pow, mul_assoc,
          â† mul_sum]
        refine mul_le_mul_of_nonneg_left ?_ (sq_nonneg _)
        conv_lhs => enter [2, i]; rw [inv_pow]
        exact I2 N
  have I4 : (âˆ‘' i, â„™ {Ï‰ | (u i * Îµ : â„) â‰¤ |S (u i) Ï‰ - ğ”¼[S (u i)]|}) < âˆ :=
    (le_of_tendsto_of_tendsto' (ENNReal.tendsto_nat_tsum _) tendsto_const_nhds I3).trans_lt
      ENNReal.ofReal_lt_top
  filter_upwards [ae_eventually_notMem I4.ne] with Ï‰ hÏ‰
  simp_rw [S, not_le, mul_comm, sum_apply] at hÏ‰
  convert hÏ‰; simp only [Y, u, sum_apply]

include hint hindep hident hnonneg in
/-- The truncation of `Xáµ¢` up to `i` satisfies the strong law of large numbers
(with respect to the truncated expectation) along the sequence
`c^n`, for any `c > 1`. This follows from `strong_law_aux1` by varying `Îµ`. -/
theorem strong_law_aux2 {c : â„} (c_one : 1 < c) :
    âˆ€áµ Ï‰, (fun n : â„• => âˆ‘ i âˆˆ range âŒŠc ^ nâŒ‹â‚Š, truncation (X i) i Ï‰ -
      ğ”¼[âˆ‘ i âˆˆ range âŒŠc ^ nâŒ‹â‚Š, truncation (X i) i]) =o[atTop] fun n : â„• => (âŒŠc ^ nâŒ‹â‚Š : â„) := by
  obtain âŸ¨v, -, v_pos, v_limâŸ© :
      âˆƒ v : â„• â†’ â„, StrictAnti v âˆ§ (âˆ€ n : â„•, 0 < v n) âˆ§ Tendsto v atTop (ğ“ 0) :=
    exists_seq_strictAnti_tendsto (0 : â„)
  have := fun i => strong_law_aux1 X hint hindep hident hnonneg c_one (v_pos i)
  filter_upwards [ae_all_iff.2 this] with Ï‰ hÏ‰
  apply Asymptotics.isLittleO_iff.2 fun Îµ Îµpos => ?_
  obtain âŸ¨i, hiâŸ© : âˆƒ i, v i < Îµ := ((tendsto_order.1 v_lim).2 Îµ Îµpos).exists
  filter_upwards [hÏ‰ i] with n hn
  simp only [Real.norm_eq_abs, Nat.abs_cast]
  exact hn.le.trans (mul_le_mul_of_nonneg_right hi.le (Nat.cast_nonneg _))

include hint hident in
/-- The expectation of the truncated version of `Xáµ¢` behaves asymptotically like the whole
expectation. This follows from convergence and CesÃ ro averaging. -/
theorem strong_law_aux3 :
    (fun n => ğ”¼[âˆ‘ i âˆˆ range n, truncation (X i) i] - n * ğ”¼[X 0]) =o[atTop] ((â†‘) : â„• â†’ â„) := by
  have A : Tendsto (fun i => ğ”¼[truncation (X i) i]) atTop (ğ“ ğ”¼[X 0]) := by
    convert (tendsto_integral_truncation hint).comp tendsto_natCast_atTop_atTop using 1
    ext i
    exact (hident i).truncation.integral_eq
  convert Asymptotics.isLittleO_sum_range_of_tendsto_zero (tendsto_sub_nhds_zero_iff.2 A) using 1
  ext1 n
  simp only [sum_sub_distrib, sum_const, card_range, nsmul_eq_mul, sum_apply, sub_left_inj]
  rw [integral_finset_sum _ fun i _ => ?_]
  exact ((hident i).symm.integrable_snd hint).1.integrable_truncation

include hint hindep hident hnonneg in
/-- The truncation of `Xáµ¢` up to `i` satisfies the strong law of large numbers
(with respect to the original expectation) along the sequence
`c^n`, for any `c > 1`. This follows from the version from the truncated expectation, and the
fact that the truncated and the original expectations have the same asymptotic behavior. -/
theorem strong_law_aux4 {c : â„} (c_one : 1 < c) :
    âˆ€áµ Ï‰, (fun n : â„• => âˆ‘ i âˆˆ range âŒŠc ^ nâŒ‹â‚Š, truncation (X i) i Ï‰ - âŒŠc ^ nâŒ‹â‚Š * ğ”¼[X 0]) =o[atTop]
    fun n : â„• => (âŒŠc ^ nâŒ‹â‚Š : â„) := by
  filter_upwards [strong_law_aux2 X hint hindep hident hnonneg c_one] with Ï‰ hÏ‰
  have A : Tendsto (fun n : â„• => âŒŠc ^ nâŒ‹â‚Š) atTop atTop :=
    tendsto_nat_floor_atTop.comp (tendsto_pow_atTop_atTop_of_one_lt c_one)
  convert hÏ‰.add ((strong_law_aux3 X hint hident).comp_tendsto A) using 1
  ext1 n
  simp

include hint hident hnonneg in
/-- The truncated and non-truncated versions of `Xáµ¢` have the same asymptotic behavior, as they
almost surely coincide at all but finitely many steps. This follows from a probability computation
and Borel-Cantelli. -/
theorem strong_law_aux5 :
    âˆ€áµ Ï‰, (fun n : â„• => âˆ‘ i âˆˆ range n, truncation (X i) i Ï‰ - âˆ‘ i âˆˆ range n, X i Ï‰) =o[atTop]
    fun n : â„• => (n : â„) := by
  have A : (âˆ‘' j : â„•, â„™ {Ï‰ | X j Ï‰ âˆˆ Set.Ioi (j : â„)}) < âˆ := by
    convert tsum_prob_mem_Ioi_lt_top hint (hnonneg 0) using 2
    ext1 j
    exact (hident j).measure_mem_eq measurableSet_Ioi
  have B : âˆ€áµ Ï‰, Tendsto (fun n : â„• => truncation (X n) n Ï‰ - X n Ï‰) atTop (ğ“ 0) := by
    filter_upwards [ae_eventually_notMem A.ne] with Ï‰ hÏ‰
    apply tendsto_const_nhds.congr' _
    filter_upwards [hÏ‰, Ioi_mem_atTop 0] with n hn npos
    simp only [truncation, indicator, Set.mem_Ioc, id, Function.comp_apply]
    split_ifs with h
    Â· exact (sub_self _).symm
    Â· have : -(n : â„) < X n Ï‰ := by
        apply lt_of_lt_of_le _ (hnonneg n Ï‰)
        simpa only [Right.neg_neg_iff, Nat.cast_pos] using npos
      simp only [this, true_and, not_le] at h
      exact (hn h).elim
  filter_upwards [B] with Ï‰ hÏ‰
  convert isLittleO_sum_range_of_tendsto_zero hÏ‰ using 1
  ext n
  rw [sum_sub_distrib]

include hint hindep hident hnonneg in
/-- `Xáµ¢` satisfies the strong law of large numbers along the sequence
`c^n`, for any `c > 1`. This follows from the version for the truncated `Xáµ¢`, and the fact that
`Xáµ¢` and its truncated version have the same asymptotic behavior. -/
theorem strong_law_aux6 {c : â„} (c_one : 1 < c) :
    âˆ€áµ Ï‰, Tendsto (fun n : â„• => (âˆ‘ i âˆˆ range âŒŠc ^ nâŒ‹â‚Š, X i Ï‰) / âŒŠc ^ nâŒ‹â‚Š) atTop (ğ“ ğ”¼[X 0]) := by
  have H : âˆ€ n : â„•, (0 : â„) < âŒŠc ^ nâŒ‹â‚Š := by
    intro n
    refine zero_lt_one.trans_le ?_
    simp only [Nat.one_le_cast, Nat.one_le_floor_iff, one_le_powâ‚€ c_one.le]
  filter_upwards [strong_law_aux4 X hint hindep hident hnonneg c_one,
    strong_law_aux5 X hint hident hnonneg] with Ï‰ hÏ‰ h'Ï‰
  rw [â† tendsto_sub_nhds_zero_iff, â† Asymptotics.isLittleO_one_iff â„]
  have L : (fun n : â„• => âˆ‘ i âˆˆ range âŒŠc ^ nâŒ‹â‚Š, X i Ï‰ - âŒŠc ^ nâŒ‹â‚Š * ğ”¼[X 0]) =o[atTop] fun n =>
      (âŒŠc ^ nâŒ‹â‚Š : â„) := by
    have A : Tendsto (fun n : â„• => âŒŠc ^ nâŒ‹â‚Š) atTop atTop :=
      tendsto_nat_floor_atTop.comp (tendsto_pow_atTop_atTop_of_one_lt c_one)
    convert hÏ‰.sub (h'Ï‰.comp_tendsto A) using 1
    ext1 n
    simp only [Function.comp_apply, sub_sub_sub_cancel_left]
  convert L.mul_isBigO (isBigO_refl (fun n : â„• => (âŒŠc ^ nâŒ‹â‚Š : â„)â»Â¹) atTop) using 1 <;>
  (ext1 n; field_simp [(H n).ne'])

include hint hindep hident hnonneg in
/-- `Xáµ¢` satisfies the strong law of large numbers along all integers. This follows from the
corresponding fact along the sequences `c^n`, and the fact that any integer can be sandwiched
between `c^n` and `c^(n+1)` with comparably small error if `c` is close enough to `1`
(which is formalized in `tendsto_div_of_monotone_of_tendsto_div_floor_pow`). -/
theorem strong_law_aux7 :
    âˆ€áµ Ï‰, Tendsto (fun n : â„• => (âˆ‘ i âˆˆ range n, X i Ï‰) / n) atTop (ğ“ ğ”¼[X 0]) := by
  obtain âŸ¨c, -, cone, climâŸ© :
      âˆƒ c : â„• â†’ â„, StrictAnti c âˆ§ (âˆ€ n : â„•, 1 < c n) âˆ§ Tendsto c atTop (ğ“ 1) :=
    exists_seq_strictAnti_tendsto (1 : â„)
  have : âˆ€ k, âˆ€áµ Ï‰,
      Tendsto (fun n : â„• => (âˆ‘ i âˆˆ range âŒŠc k ^ nâŒ‹â‚Š, X i Ï‰) / âŒŠc k ^ nâŒ‹â‚Š) atTop (ğ“ ğ”¼[X 0]) :=
    fun k => strong_law_aux6 X hint hindep hident hnonneg (cone k)
  filter_upwards [ae_all_iff.2 this] with Ï‰ hÏ‰
  apply tendsto_div_of_monotone_of_tendsto_div_floor_pow _ _ _ c cone clim _
  Â· intro m n hmn
    exact sum_le_sum_of_subset_of_nonneg (range_mono hmn) fun i _ _ => hnonneg i Ï‰
  Â· exact hÏ‰

end StrongLawNonneg

/-- **Strong law of large numbers**, almost sure version: if `X n` is a sequence of independent
identically distributed integrable real-valued random variables, then `âˆ‘ i âˆˆ range n, X i / n`
converges almost surely to `ğ”¼[X 0]`. We give here the strong version, due to Etemadi, that only
requires pairwise independence. Superseded by `strong_law_ae`, which works for random variables
taking values in any Banach space. -/
theorem strong_law_ae_real {Î© : Type*} {m : MeasurableSpace Î©} {Î¼ : Measure Î©}
    (X : â„• â†’ Î© â†’ â„) (hint : Integrable (X 0) Î¼)
    (hindep : Pairwise ((IndepFun Â· Â· Î¼) on X))
    (hident : âˆ€ i, IdentDistrib (X i) (X 0) Î¼ Î¼) :
    âˆ€áµ Ï‰ âˆ‚Î¼, Tendsto (fun n : â„• => (âˆ‘ i âˆˆ range n, X i Ï‰) / n) atTop (ğ“ Î¼[X 0]) := by
  let mÎ© : MeasureSpace Î© := âŸ¨Î¼âŸ©
  -- first get rid of the trivial case where the space is not a probability space
  by_cases h : âˆ€áµ Ï‰, X 0 Ï‰ = 0
  Â· have I : âˆ€áµ Ï‰, âˆ€ i, X i Ï‰ = 0 := by
      rw [ae_all_iff]
      intro i
      exact (hident i).symm.ae_snd (p := fun x â†¦ x = 0) measurableSet_eq h
    filter_upwards [I] with Ï‰ hÏ‰
    simpa [hÏ‰] using (integral_eq_zero_of_ae h).symm
  have : IsProbabilityMeasure Î¼ :=
    hint.isProbabilityMeasure_of_indepFun (X 0) (X 1) h (hindep zero_ne_one)
  -- then consider separately the positive and the negative part, and apply the result
  -- for nonnegative functions to them.
  let pos : â„ â†’ â„ := fun x => max x 0
  let neg : â„ â†’ â„ := fun x => max (-x) 0
  have posm : Measurable pos := measurable_id'.max measurable_const
  have negm : Measurable neg := measurable_id'.neg.max measurable_const
  have A : âˆ€áµ Ï‰, Tendsto (fun n : â„• => (âˆ‘ i âˆˆ range n, (pos âˆ˜ X i) Ï‰) / n) atTop (ğ“ ğ”¼[pos âˆ˜ X 0]) :=
    strong_law_aux7 _ hint.pos_part (fun i j hij => (hindep hij).comp posm posm)
      (fun i => (hident i).comp posm) fun i Ï‰ => le_max_right _ _
  have B : âˆ€áµ Ï‰, Tendsto (fun n : â„• => (âˆ‘ i âˆˆ range n, (neg âˆ˜ X i) Ï‰) / n) atTop (ğ“ ğ”¼[neg âˆ˜ X 0]) :=
    strong_law_aux7 _ hint.neg_part (fun i j hij => (hindep hij).comp negm negm)
      (fun i => (hident i).comp negm) fun i Ï‰ => le_max_right _ _
  filter_upwards [A, B] with Ï‰ hÏ‰pos hÏ‰neg
  convert hÏ‰pos.sub hÏ‰neg using 2
  Â· simp only [pos, neg, â† sub_div, â† sum_sub_distrib, max_zero_sub_max_neg_zero_eq_self,
      Function.comp_apply]
  Â· simp only [pos, neg, â† integral_sub hint.pos_part hint.neg_part,
      max_zero_sub_max_neg_zero_eq_self, Function.comp_apply, mÎ©]

end StrongLawAeReal

section StrongLawVectorSpace

variable {Î© : Type*} {mÎ© : MeasurableSpace Î©} {Î¼ : Measure Î©} [IsProbabilityMeasure Î¼]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [CompleteSpace E]
  [MeasurableSpace E]

open Set TopologicalSpace

/-- Preliminary lemma for the strong law of large numbers for vector-valued random variables:
the composition of the random variables with a simple function satisfies the strong law of large
numbers. -/
lemma strong_law_ae_simpleFunc_comp (X : â„• â†’ Î© â†’ E) (h' : Measurable (X 0))
    (hindep : Pairwise ((IndepFun Â· Â· Î¼) on X))
    (hident : âˆ€ i, IdentDistrib (X i) (X 0) Î¼ Î¼) (Ï† : SimpleFunc E E) :
    âˆ€áµ Ï‰ âˆ‚Î¼,
      Tendsto (fun n : â„• â†¦ (n : â„) â»Â¹ â€¢ (âˆ‘ i âˆˆ range n, Ï† (X i Ï‰))) atTop (ğ“ Î¼[Ï† âˆ˜ (X 0)]) := by
  -- this follows from the one-dimensional version when `Ï†` takes a single value, and is then
  -- extended to the general case by linearity.
  classical
  refine SimpleFunc.induction (motive := fun Ïˆ â†¦ âˆ€áµ Ï‰ âˆ‚Î¼,
    Tendsto (fun n : â„• â†¦ (n : â„) â»Â¹ â€¢ (âˆ‘ i âˆˆ range n, Ïˆ (X i Ï‰))) atTop (ğ“ Î¼[Ïˆ âˆ˜ (X 0)])) ?_ ?_ Ï†
  Â· intro c s hs
    simp only [SimpleFunc.const_zero, SimpleFunc.coe_piecewise, SimpleFunc.coe_const,
      SimpleFunc.coe_zero, piecewise_eq_indicator, Function.comp_apply]
    let F : E â†’ â„ := indicator s 1
    have F_meas : Measurable F := (measurable_indicator_const_iff 1).2 hs
    let Y : â„• â†’ Î© â†’ â„ := fun n â†¦ F âˆ˜ (X n)
    have : âˆ€áµ (Ï‰ : Î©) âˆ‚Î¼, Tendsto (fun (n : â„•) â†¦ (n : â„)â»Â¹ â€¢ âˆ‘ i âˆˆ Finset.range n, Y i Ï‰)
        atTop (ğ“ Î¼[Y 0]) := by
      simp only [smul_eq_mul, â† div_eq_inv_mul]
      apply strong_law_ae_real
      Â· exact SimpleFunc.integrable_of_isFiniteMeasure
          ((SimpleFunc.piecewise s hs (SimpleFunc.const _ (1 : â„))
            (SimpleFunc.const _ (0 : â„))).comp (X 0) h')
      Â· exact fun i j hij â†¦ IndepFun.comp (hindep hij) F_meas F_meas
      Â· exact fun i â†¦ (hident i).comp F_meas
    filter_upwards [this] with Ï‰ hÏ‰
    have I : indicator s (Function.const E c) = (fun x â†¦ (indicator s (1 : E â†’ â„) x) â€¢ c) := by
      ext
      rw [â† indicator_smul_const_apply]
      congr! 1
      ext
      simp
    simp only [I, integral_smul_const]
    convert Tendsto.smul_const hÏ‰ c using 1
    simp [F, Y, â† sum_smul, smul_smul]
  Â· rintro Ï† Ïˆ - hÏ† hÏˆ
    filter_upwards [hÏ†, hÏˆ] with Ï‰ hÏ‰Ï† hÏ‰Ïˆ
    convert hÏ‰Ï†.add hÏ‰Ïˆ using 1
    Â· simp [sum_add_distrib]
    Â· congr 1
      rw [â† integral_add]
      Â· rfl
      Â· exact (Ï†.comp (X 0) h').integrable_of_isFiniteMeasure
      Â· exact (Ïˆ.comp (X 0) h').integrable_of_isFiniteMeasure

variable [BorelSpace E]

/-- Preliminary lemma for the strong law of large numbers for vector-valued random variables,
assuming measurability in addition to integrability. This is weakened to ae measurability in
the full version `ProbabilityTheory.strong_law_ae`. -/
lemma strong_law_ae_of_measurable
    (X : â„• â†’ Î© â†’ E) (hint : Integrable (X 0) Î¼) (h' : StronglyMeasurable (X 0))
    (hindep : Pairwise ((IndepFun Â· Â· Î¼) on X))
    (hident : âˆ€ i, IdentDistrib (X i) (X 0) Î¼ Î¼) :
    âˆ€áµ Ï‰ âˆ‚Î¼, Tendsto (fun n : â„• â†¦ (n : â„) â»Â¹ â€¢ (âˆ‘ i âˆˆ range n, X i Ï‰)) atTop (ğ“ Î¼[X 0]) := by
  /- Choose a simple function `Ï†` such that `Ï† (X 0)` approximates well enough `X 0` -- this is
  possible as `X 0` is strongly measurable. Then `Ï† (X n)` approximates well `X n`.
  Then the strong law for `Ï† (X n)` implies the strong law for `X n`, up to a small
  error controlled by `nâ»Â¹ âˆ‘_{i=0}^{n-1} â€–X i - Ï† (X i)â€–`. This one is also controlled thanks
  to the one-dimensional law of large numbers: it converges ae to `ğ”¼[â€–X 0 - Ï† (X 0)â€–]`, which
  is arbitrarily small for well-chosen `Ï†`. -/
  let s : Set E := Set.range (X 0) âˆª {0}
  have zero_s : 0 âˆˆ s := by simp [s]
  have : SeparableSpace s := h'.separableSpace_range_union_singleton
  have : Nonempty s := âŸ¨0, zero_sâŸ©
  -- sequence of approximating simple functions.
  let Ï† : â„• â†’ SimpleFunc E E :=
    SimpleFunc.nearestPt (fun k => Nat.casesOn k 0 ((â†‘) âˆ˜ denseSeq s) : â„• â†’ E)
  let Y : â„• â†’ â„• â†’ Î© â†’ E := fun k i â†¦ (Ï† k) âˆ˜ (X i)
  -- strong law for `Ï† (X n)`
  have A : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ k,
      Tendsto (fun n : â„• â†¦ (n : â„) â»Â¹ â€¢ (âˆ‘ i âˆˆ range n, Y k i Ï‰)) atTop (ğ“ Î¼[Y k 0]) :=
    ae_all_iff.2 (fun k â†¦ strong_law_ae_simpleFunc_comp X h'.measurable hindep hident (Ï† k))
  -- strong law for the error `â€–X i - Ï† (X i)â€–`
  have B : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ k, Tendsto (fun n : â„• â†¦ (âˆ‘ i âˆˆ range n, â€–(X i - Y k i) Ï‰â€–) / n)
        atTop (ğ“ Î¼[(fun Ï‰ â†¦ â€–(X 0 - Y k 0) Ï‰â€–)]) := by
    apply ae_all_iff.2 (fun k â†¦ ?_)
    let G : â„• â†’ E â†’ â„ := fun k x â†¦ â€–x - Ï† k xâ€–
    have G_meas : âˆ€ k, Measurable (G k) :=
      fun k â†¦ (measurable_id.sub_stronglyMeasurable (Ï† k).stronglyMeasurable).norm
    have I : âˆ€ k i, (fun Ï‰ â†¦ â€–(X i - Y k i) Ï‰â€–) = (G k) âˆ˜ (X i) := fun k i â†¦ rfl
    apply strong_law_ae_real (fun i Ï‰ â†¦ â€–(X i - Y k i) Ï‰â€–)
    Â· exact (hint.sub ((Ï† k).comp (X 0) h'.measurable).integrable_of_isFiniteMeasure).norm
    Â· unfold Function.onFun
      simp_rw [I]
      intro i j hij
      exact (hindep hij).comp (G_meas k) (G_meas k)
    Â· intro i
      simp_rw [I]
      apply (hident i).comp (G_meas k)
  -- check that, when both convergences above hold, then the strong law is satisfied
  filter_upwards [A, B] with Ï‰ hÏ‰ h'Ï‰
  rw [tendsto_iff_norm_sub_tendsto_zero, tendsto_order]
  refine âŸ¨fun c hc â†¦ Eventually.of_forall (fun n â†¦ hc.trans_le (norm_nonneg _)), ?_âŸ©
  -- start with some positive `Îµ` (the desired precision), and fix `Î´` with `3 Î´ < Îµ`.
  intro Îµ (Îµpos : 0 < Îµ)
  obtain âŸ¨Î´, Î´pos, hÎ´âŸ© : âˆƒ Î´, 0 < Î´ âˆ§ Î´ + Î´ + Î´ < Îµ := âŸ¨Îµ/4, by positivity, by linarithâŸ©
  -- choose `k` large enough so that `Ï†â‚– (X 0)` approximates well enough `X 0`, up to the
  -- precision `Î´`.
  obtain âŸ¨k, hkâŸ© : âˆƒ k, âˆ« Ï‰, â€–(X 0 - Y k 0) Ï‰â€– âˆ‚Î¼ < Î´ := by
    simp_rw [Pi.sub_apply, norm_sub_rev (X 0 _)]
    exact ((tendsto_order.1 (tendsto_integral_norm_approxOn_sub h'.measurable hint)).2 Î´
      Î´pos).exists
  have : â€–Î¼[Y k 0] - Î¼[X 0]â€– < Î´ := by
    rw [norm_sub_rev, â† integral_sub hint]
    Â· exact (norm_integral_le_integral_norm _).trans_lt hk
    Â· exact ((Ï† k).comp (X 0) h'.measurable).integrable_of_isFiniteMeasure
  -- consider `n` large enough for which the above convergences have taken place within `Î´`.
  have I : âˆ€á¶  n in atTop, (âˆ‘ i âˆˆ range n, â€–(X i - Y k i) Ï‰â€–) / n < Î´ :=
    (tendsto_order.1 (h'Ï‰ k)).2 Î´ hk
  have J : âˆ€á¶  (n : â„•) in atTop, â€–(n : â„) â»Â¹ â€¢ (âˆ‘ i âˆˆ range n, Y k i Ï‰) - Î¼[Y k 0]â€– < Î´ := by
    specialize hÏ‰ k
    rw [tendsto_iff_norm_sub_tendsto_zero] at hÏ‰
    exact (tendsto_order.1 hÏ‰).2 Î´ Î´pos
  filter_upwards [I, J] with n hn h'n
  -- at such an `n`, the strong law is realized up to `Îµ`.
  calc
  â€–(n : â„)â»Â¹ â€¢ âˆ‘ i âˆˆ Finset.range n, X i Ï‰ - Î¼[X 0]â€–
    = â€–(n : â„)â»Â¹ â€¢ âˆ‘ i âˆˆ Finset.range n, (X i Ï‰ - Y k i Ï‰) +
        ((n : â„)â»Â¹ â€¢ âˆ‘ i âˆˆ Finset.range n, Y k i Ï‰ - Î¼[Y k 0]) + (Î¼[Y k 0] - Î¼[X 0])â€– := by
      congr
      simp only [sum_sub_distrib, smul_sub]
      abel
  _ â‰¤ â€–(n : â„)â»Â¹ â€¢ âˆ‘ i âˆˆ Finset.range n, (X i Ï‰ - Y k i Ï‰)â€– +
        â€–(n : â„)â»Â¹ â€¢ âˆ‘ i âˆˆ Finset.range n, Y k i Ï‰ - Î¼[Y k 0]â€– + â€–Î¼[Y k 0] - Î¼[X 0]â€– :=
      norm_addâ‚ƒ_le
  _ â‰¤ (âˆ‘ i âˆˆ Finset.range n, â€–X i Ï‰ - Y k i Ï‰â€–) / n + Î´ + Î´ := by
      gcongr
      simp only [norm_smul, norm_inv, RCLike.norm_natCast,
        div_eq_inv_mul]
      gcongr
      exact norm_sum_le _ _
  _ â‰¤ Î´ + Î´ + Î´ := by
      gcongr
      exact hn.le
  _ < Îµ := hÎ´

omit [IsProbabilityMeasure Î¼] in
/-- **Strong law of large numbers**, almost sure version: if `X n` is a sequence of independent
identically distributed integrable random variables taking values in a Banach space,
then `nâ»Â¹ â€¢ âˆ‘ i âˆˆ range n, X i` converges almost surely to `ğ”¼[X 0]`. We give here the strong
version, due to Etemadi, that only requires pairwise independence. -/
theorem strong_law_ae (X : â„• â†’ Î© â†’ E) (hint : Integrable (X 0) Î¼)
    (hindep : Pairwise ((IndepFun Â· Â· Î¼) on X))
    (hident : âˆ€ i, IdentDistrib (X i) (X 0) Î¼ Î¼) :
    âˆ€áµ Ï‰ âˆ‚Î¼, Tendsto (fun n : â„• â†¦ (n : â„) â»Â¹ â€¢ (âˆ‘ i âˆˆ range n, X i Ï‰)) atTop (ğ“ Î¼[X 0]) := by
  -- First exclude the trivial case where the space is not a probability space
  by_cases h : âˆ€áµ Ï‰ âˆ‚Î¼, X 0 Ï‰ = 0
  Â· have I : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ i, X i Ï‰ = 0 := by
      rw [ae_all_iff]
      intro i
      exact (hident i).symm.ae_snd (p := fun x â†¦ x = 0) measurableSet_eq h
    filter_upwards [I] with Ï‰ hÏ‰
    simpa [hÏ‰] using (integral_eq_zero_of_ae h).symm
  have : IsProbabilityMeasure Î¼ :=
    hint.isProbabilityMeasure_of_indepFun (X 0) (X 1) h (hindep zero_ne_one)
  -- we reduce to the case of strongly measurable random variables, by using `Y i` which is strongly
  -- measurable and ae equal to `X i`.
  have A : âˆ€ i, Integrable (X i) Î¼ := fun i â†¦ (hident i).integrable_iff.2 hint
  let Y : â„• â†’ Î© â†’ E := fun i â†¦ (A i).1.mk (X i)
  have B : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ n, X n Ï‰ = Y n Ï‰ :=
    ae_all_iff.2 (fun i â†¦ AEStronglyMeasurable.ae_eq_mk (A i).1)
  have Yint : Integrable (Y 0) Î¼ := Integrable.congr hint (AEStronglyMeasurable.ae_eq_mk (A 0).1)
  have C : âˆ€áµ Ï‰ âˆ‚Î¼,
      Tendsto (fun n : â„• â†¦ (n : â„) â»Â¹ â€¢ (âˆ‘ i âˆˆ range n, Y i Ï‰)) atTop (ğ“ Î¼[Y 0]) := by
    apply strong_law_ae_of_measurable Y Yint ((A 0).1.stronglyMeasurable_mk)
      (fun i j hij â†¦ IndepFun.congr (hindep hij) (A i).1.ae_eq_mk (A j).1.ae_eq_mk)
      (fun i â†¦ ((A i).1.identDistrib_mk.symm.trans (hident i)).trans (A 0).1.identDistrib_mk)
  filter_upwards [B, C] with Ï‰ hâ‚ hâ‚‚
  have : Î¼[X 0] = Î¼[Y 0] := integral_congr_ae (AEStronglyMeasurable.ae_eq_mk (A 0).1)
  rw [this]
  apply Tendsto.congr (fun n â†¦ ?_) hâ‚‚
  congr with i
  exact (hâ‚ i).symm

end StrongLawVectorSpace

section StrongLawLp

variable {Î© : Type*} {mÎ© : MeasurableSpace Î©} {Î¼ : Measure Î©}
  {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [CompleteSpace E]
  [MeasurableSpace E] [BorelSpace E]

/-- **Strong law of large numbers**, Láµ– version: if `X n` is a sequence of independent
identically distributed random variables in Láµ–, then `nâ»Â¹ â€¢ âˆ‘ i âˆˆ range n, X i`
converges in `Láµ–` to `ğ”¼[X 0]`. -/
theorem strong_law_Lp {p : â„â‰¥0âˆ} (hp : 1 â‰¤ p) (hp' : p â‰  âˆ) (X : â„• â†’ Î© â†’ E)
    (hâ„’p : MemLp (X 0) p Î¼) (hindep : Pairwise ((IndepFun Â· Â· Î¼) on X))
    (hident : âˆ€ i, IdentDistrib (X i) (X 0) Î¼ Î¼) :
    Tendsto (fun (n : â„•) => eLpNorm (fun Ï‰ => (n : â„) â»Â¹ â€¢ (âˆ‘ i âˆˆ range n, X i Ï‰) - Î¼[X 0]) p Î¼)
      atTop (ğ“ 0) := by
  -- First exclude the trivial case where the space is not a probability space
  by_cases h : âˆ€áµ Ï‰ âˆ‚Î¼, X 0 Ï‰ = 0
  Â· have I : âˆ€áµ Ï‰ âˆ‚Î¼, âˆ€ i, X i Ï‰ = 0 := by
      rw [ae_all_iff]
      intro i
      exact (hident i).symm.ae_snd (p := fun x â†¦ x = 0) measurableSet_eq h
    have A (n : â„•) : eLpNorm (fun Ï‰ => (n : â„) â»Â¹ â€¢ (âˆ‘ i âˆˆ range n, X i Ï‰) - Î¼[X 0]) p Î¼ = 0 := by
      simp only [integral_eq_zero_of_ae h, sub_zero]
      apply eLpNorm_eq_zero_of_ae_zero
      filter_upwards [I] with Ï‰ hÏ‰
      simp [hÏ‰]
    simp [A]
  -- Then use ae convergence and uniform integrability
  have : IsProbabilityMeasure Î¼ := MemLp.isProbabilityMeasure_of_indepFun
    (X 0) (X 1) (zero_lt_one.trans_le hp).ne' hp' hâ„’p h (hindep zero_ne_one)
  have hmeas : âˆ€ i, AEStronglyMeasurable (X i) Î¼ := fun i =>
    (hident i).aestronglyMeasurable_iff.2 hâ„’p.1
  have hint : Integrable (X 0) Î¼ := hâ„’p.integrable hp
  have havg (n : â„•) :
      AEStronglyMeasurable (fun Ï‰ => (n : â„) â»Â¹ â€¢ (âˆ‘ i âˆˆ range n, X i Ï‰)) Î¼ :=
    AEStronglyMeasurable.const_smul (aestronglyMeasurable_fun_sum _ fun i _ => hmeas i) _
  refine tendsto_Lp_finite_of_tendstoInMeasure hp hp' havg (memLp_const _) ?_
    (tendstoInMeasure_of_tendsto_ae havg (strong_law_ae _ hint hindep hident))
  rw [(_ : (fun (n : â„•) Ï‰ => (n : â„)â»Â¹ â€¢ (âˆ‘ i âˆˆ range n, X i Ï‰))
            = fun (n : â„•) => (n : â„)â»Â¹ â€¢ (âˆ‘ i âˆˆ range n, X i))]
  Â· apply UniformIntegrable.unifIntegrable
    apply uniformIntegrable_average hp
    exact MemLp.uniformIntegrable_of_identDistrib hp hp' hâ„’p hident
  Â· ext n Ï‰
    simp only [Pi.smul_apply, sum_apply]

end StrongLawLp

end ProbabilityTheory
