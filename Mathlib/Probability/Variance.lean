/-
Copyright (c) 2022 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Kexing Ying
-/
import Mathlib.Probability.Notation
import Mathlib.Probability.Integration
import Mathlib.MeasureTheory.Function.L2Space

/-!
# Variance of random variables

We define the variance of a real-valued random variable as `Var[X] = ğ”¼[(X - ğ”¼[X])^2]` (in the
`ProbabilityTheory` locale).

## Main definitions

* `ProbabilityTheory.evariance`: the variance of a real-valued random variable as an extended
  non-negative real.
* `ProbabilityTheory.variance`: the variance of a real-valued random variable as a real number.

## Main results

* `ProbabilityTheory.variance_le_expectation_sq`: the inequality `Var[X] â‰¤ ğ”¼[X^2]`.
* `ProbabilityTheory.meas_ge_le_variance_div_sq`: Chebyshev's inequality, i.e.,
      `â„™ {Ï‰ | c â‰¤ |X Ï‰ - ğ”¼[X]|} â‰¤ ENNReal.ofReal (Var[X] / c ^ 2)`.
* `ProbabilityTheory.meas_ge_le_evariance_div_sq`: Chebyshev's inequality formulated with
  `evariance` without requiring the random variables to be LÂ².
* `ProbabilityTheory.IndepFun.variance_add`: the variance of the sum of two independent
  random variables is the sum of the variances.
* `ProbabilityTheory.IndepFun.variance_sum`: the variance of a finite sum of pairwise
  independent random variables is the sum of the variances.
* `ProbabilityTheory.variance_le_sub_mul_sub`: the variance of a random variable `X` satisfying
  `a â‰¤ X â‰¤ b` almost everywhere is at most `(b - ğ”¼ X) * (ğ”¼ X - a)`.
* `ProbabilityTheory.variance_le_sq_of_bounded`: the variance of a random variable `X` satisfying
  `a â‰¤ X â‰¤ b` almost everywhere is at most`((b - a) / 2) ^ 2`.
-/

open MeasureTheory Filter Finset

noncomputable section

open scoped MeasureTheory ProbabilityTheory ENNReal NNReal

namespace ProbabilityTheory

-- Porting note: Consider if `evariance` or `eVariance` is better. Also,
-- consider `eVariationOn` in `Mathlib.Analysis.BoundedVariation`.
/-- The `â„â‰¥0âˆ`-valued variance of a real-valued random variable defined as the Lebesgue integral of
`(X - ğ”¼[X])^2`. -/
def evariance {Î© : Type*} {_ : MeasurableSpace Î©} (X : Î© â†’ â„) (Î¼ : Measure Î©) : â„â‰¥0âˆ :=
  âˆ«â» Ï‰, (â€–X Ï‰ - Î¼[X]â€–â‚Š : â„â‰¥0âˆ) ^ 2 âˆ‚Î¼

/-- The `â„`-valued variance of a real-valued random variable defined by applying `ENNReal.toReal`
to `evariance`. -/
def variance {Î© : Type*} {_ : MeasurableSpace Î©} (X : Î© â†’ â„) (Î¼ : Measure Î©) : â„ :=
  (evariance X Î¼).toReal

variable {Î© : Type*} {m : MeasurableSpace Î©} {X : Î© â†’ â„} {Î¼ : Measure Î©}

theorem _root_.MeasureTheory.Memâ„’p.evariance_lt_top [IsFiniteMeasure Î¼] (hX : Memâ„’p X 2 Î¼) :
    evariance X Î¼ < âˆ := by
  have := ENNReal.pow_lt_top (hX.sub <| memâ„’p_const <| Î¼[X]).2 2
  rw [eLpNorm_eq_lintegral_rpow_nnnorm two_ne_zero ENNReal.two_ne_top, â† ENNReal.rpow_two] at this
  simp only [ENNReal.toReal_ofNat, Pi.sub_apply, ENNReal.one_toReal, one_div] at this
  rw [â† ENNReal.rpow_mul, inv_mul_cancelâ‚€ (two_ne_zero : (2 : â„) â‰  0), ENNReal.rpow_one] at this
  simp_rw [ENNReal.rpow_two] at this
  exact this

theorem evariance_eq_top [IsFiniteMeasure Î¼] (hXm : AEStronglyMeasurable X Î¼) (hX : Â¬Memâ„’p X 2 Î¼) :
    evariance X Î¼ = âˆ := by
  by_contra h
  rw [â† Ne, â† lt_top_iff_ne_top] at h
  have : Memâ„’p (fun Ï‰ => X Ï‰ - Î¼[X]) 2 Î¼ := by
    refine âŸ¨hXm.sub aestronglyMeasurable_const, ?_âŸ©
    rw [eLpNorm_eq_lintegral_rpow_nnnorm two_ne_zero ENNReal.two_ne_top]
    simp only [ENNReal.toReal_ofNat, ENNReal.one_toReal, ENNReal.rpow_two, Ne]
    exact ENNReal.rpow_lt_top_of_nonneg (by linarith) h.ne
  refine hX ?_
  convert this.add (memâ„’p_const Î¼[X])
  ext Ï‰
  rw [Pi.add_apply, sub_add_cancel]

theorem evariance_lt_top_iff_memâ„’p [IsFiniteMeasure Î¼] (hX : AEStronglyMeasurable X Î¼) :
    evariance X Î¼ < âˆ â†” Memâ„’p X 2 Î¼ := by
  refine âŸ¨?_, MeasureTheory.Memâ„’p.evariance_lt_topâŸ©
  contrapose
  rw [not_lt, top_le_iff]
  exact evariance_eq_top hX

theorem _root_.MeasureTheory.Memâ„’p.ofReal_variance_eq [IsFiniteMeasure Î¼] (hX : Memâ„’p X 2 Î¼) :
    ENNReal.ofReal (variance X Î¼) = evariance X Î¼ := by
  rw [variance, ENNReal.ofReal_toReal]
  exact hX.evariance_lt_top.ne

theorem evariance_eq_lintegral_ofReal (X : Î© â†’ â„) (Î¼ : Measure Î©) :
    evariance X Î¼ = âˆ«â» Ï‰, ENNReal.ofReal ((X Ï‰ - Î¼[X]) ^ 2) âˆ‚Î¼ := by
  rw [evariance]
  congr
  ext1 Ï‰
  rw [pow_two, â† ENNReal.coe_mul, â† nnnorm_mul, â† pow_two]
  congr
  exact (Real.toNNReal_eq_nnnorm_of_nonneg <| sq_nonneg _).symm

theorem _root_.MeasureTheory.Memâ„’p.variance_eq_of_integral_eq_zero (hX : Memâ„’p X 2 Î¼)
    (hXint : Î¼[X] = 0) : variance X Î¼ = Î¼[X ^ (2 : Nat)] := by
  rw [variance, evariance_eq_lintegral_ofReal, â† ofReal_integral_eq_lintegral_ofReal,
      ENNReal.toReal_ofReal (by positivity)] <;>
    simp_rw [hXint, sub_zero]
  Â· rfl
  Â· convert hX.integrable_norm_rpow two_ne_zero ENNReal.two_ne_top with Ï‰
    simp only [Pi.sub_apply, Real.norm_eq_abs, ENNReal.toReal_ofNat, ENNReal.one_toReal,
      Real.rpow_two, sq_abs, abs_pow]
  Â· exact ae_of_all _ fun Ï‰ => pow_two_nonneg _

theorem _root_.MeasureTheory.Memâ„’p.variance_eq [IsFiniteMeasure Î¼] (hX : Memâ„’p X 2 Î¼) :
    variance X Î¼ = Î¼[(X - fun _ => Î¼[X] :) ^ (2 : Nat)] := by
  rw [variance, evariance_eq_lintegral_ofReal, â† ofReal_integral_eq_lintegral_ofReal,
    ENNReal.toReal_ofReal (by positivity)]
  Â· rfl
  Â· convert (hX.sub <| memâ„’p_const Î¼[X]).integrable_norm_rpow two_ne_zero ENNReal.two_ne_top
      with Ï‰
    simp only [Pi.sub_apply, Real.norm_eq_abs, ENNReal.toReal_ofNat, ENNReal.one_toReal,
      Real.rpow_two, sq_abs, abs_pow]
  Â· exact ae_of_all _ fun Ï‰ => pow_two_nonneg _

@[simp]
theorem evariance_zero : evariance 0 Î¼ = 0 := by simp [evariance]

theorem evariance_eq_zero_iff (hX : AEMeasurable X Î¼) :
    evariance X Î¼ = 0 â†” X =áµ[Î¼] fun _ => Î¼[X] := by
  rw [evariance, lintegral_eq_zero_iff']
  constructor <;> intro hX <;> filter_upwards [hX] with Ï‰ hÏ‰
  Â· simpa only [Pi.zero_apply, sq_eq_zero_iff, ENNReal.coe_eq_zero, nnnorm_eq_zero, sub_eq_zero]
      using hÏ‰
  Â· rw [hÏ‰]
    simp
  Â· exact (hX.sub_const _).ennnorm.pow_const _ -- TODO `measurability` and `fun_prop` fail

theorem evariance_mul (c : â„) (X : Î© â†’ â„) (Î¼ : Measure Î©) :
    evariance (fun Ï‰ => c * X Ï‰) Î¼ = ENNReal.ofReal (c ^ 2) * evariance X Î¼ := by
  rw [evariance, evariance, â† lintegral_const_mul' _ _ ENNReal.ofReal_lt_top.ne]
  congr
  ext1 Ï‰
  rw [ENNReal.ofReal, â† ENNReal.coe_pow, â† ENNReal.coe_pow, â† ENNReal.coe_mul]
  congr
  rw [â† sq_abs, â† Real.rpow_two, Real.toNNReal_rpow_of_nonneg (abs_nonneg _), NNReal.rpow_two,
    â† mul_pow, Real.toNNReal_mul_nnnorm _ (abs_nonneg _)]
  conv_rhs => rw [â† nnnorm_norm, norm_mul, norm_abs_eq_norm, â† norm_mul, nnnorm_norm, mul_sub]
  congr
  rw [mul_comm]
  simp_rw [â† smul_eq_mul, â† integral_smul_const, smul_eq_mul, mul_comm]

scoped notation "eVar[" X "]" => ProbabilityTheory.evariance X MeasureTheory.MeasureSpace.volume

@[simp]
theorem variance_zero (Î¼ : Measure Î©) : variance 0 Î¼ = 0 := by
  simp only [variance, evariance_zero, ENNReal.zero_toReal]

theorem variance_nonneg (X : Î© â†’ â„) (Î¼ : Measure Î©) : 0 â‰¤ variance X Î¼ :=
  ENNReal.toReal_nonneg

theorem variance_mul (c : â„) (X : Î© â†’ â„) (Î¼ : Measure Î©) :
    variance (fun Ï‰ => c * X Ï‰) Î¼ = c ^ 2 * variance X Î¼ := by
  rw [variance, evariance_mul, ENNReal.toReal_mul, ENNReal.toReal_ofReal (sq_nonneg _)]
  rfl

theorem variance_smul (c : â„) (X : Î© â†’ â„) (Î¼ : Measure Î©) :
    variance (c â€¢ X) Î¼ = c ^ 2 * variance X Î¼ :=
  variance_mul c X Î¼

theorem variance_smul' {A : Type*} [CommSemiring A] [Algebra A â„] (c : A) (X : Î© â†’ â„)
    (Î¼ : Measure Î©) : variance (c â€¢ X) Î¼ = c ^ 2 â€¢ variance X Î¼ := by
  convert variance_smul (algebraMap A â„ c) X Î¼ using 1
  Â· congr; simp only [algebraMap_smul]
  Â· simp only [Algebra.smul_def, map_pow]

scoped notation "Var[" X "]" => ProbabilityTheory.variance X MeasureTheory.MeasureSpace.volume

theorem variance_def' [IsProbabilityMeasure Î¼] {X : Î© â†’ â„} (hX : Memâ„’p X 2 Î¼) :
    variance X Î¼ = Î¼[X ^ 2] - Î¼[X] ^ 2 := by
  rw [hX.variance_eq, sub_sq', integral_sub', integral_add']; rotate_left
  Â· exact hX.integrable_sq
  Â· apply integrable_const
  Â· apply hX.integrable_sq.add
    apply integrable_const
  Â· exact ((hX.integrable one_le_two).const_mul 2).mul_const' _
  simp only [Pi.pow_apply, integral_const, measure_univ, ENNReal.one_toReal, smul_eq_mul, one_mul,
    Pi.mul_apply, Pi.ofNat_apply, Nat.cast_ofNat, integral_mul_right, integral_mul_left]
  ring

theorem variance_le_expectation_sq [IsProbabilityMeasure Î¼] {X : Î© â†’ â„}
    (hm : AEStronglyMeasurable X Î¼) : variance X Î¼ â‰¤ Î¼[X ^ 2] := by
  by_cases hX : Memâ„’p X 2 Î¼
  Â· rw [variance_def' hX]
    simp only [sq_nonneg, sub_le_self_iff]
  rw [variance, evariance_eq_lintegral_ofReal, â† integral_eq_lintegral_of_nonneg_ae]
  Â· by_cases hint : Integrable X Î¼; swap
    Â· simp only [integral_undef hint, Pi.pow_apply, Pi.sub_apply, sub_zero]
      exact le_rfl
    Â· rw [integral_undef]
      Â· exact integral_nonneg fun a => sq_nonneg _
      intro h
      have A : Memâ„’p (X - fun Ï‰ : Î© => Î¼[X]) 2 Î¼ :=
        (memâ„’p_two_iff_integrable_sq (hint.aestronglyMeasurable.sub aestronglyMeasurable_const)).2 h
      have B : Memâ„’p (fun _ : Î© => Î¼[X]) 2 Î¼ := memâ„’p_const _
      apply hX
      convert A.add B
      simp
  Â· exact Eventually.of_forall fun x => sq_nonneg _
  Â· exact (AEMeasurable.pow_const (hm.aemeasurable.sub_const _) _).aestronglyMeasurable

theorem evariance_def' [IsProbabilityMeasure Î¼] {X : Î© â†’ â„} (hX : AEStronglyMeasurable X Î¼) :
    evariance X Î¼ = (âˆ«â» Ï‰, (â€–X Ï‰â€–â‚Š ^ 2 :) âˆ‚Î¼) - ENNReal.ofReal (Î¼[X] ^ 2) := by
  by_cases hâ„’ : Memâ„’p X 2 Î¼
  Â· rw [â† hâ„’.ofReal_variance_eq, variance_def' hâ„’, ENNReal.ofReal_sub _ (sq_nonneg _)]
    congr
    rw [lintegral_coe_eq_integral]
    Â· congr 2 with Ï‰
      simp only [Pi.pow_apply, NNReal.coe_pow, coe_nnnorm, Real.norm_eq_abs, Even.pow_abs even_two]
    Â· exact hâ„’.abs.integrable_sq
  Â· symm
    rw [evariance_eq_top hX hâ„’, ENNReal.sub_eq_top_iff]
    refine âŸ¨?_, ENNReal.ofReal_ne_topâŸ©
    rw [Memâ„’p, not_and] at hâ„’
    specialize hâ„’ hX
    simp only [eLpNorm_eq_lintegral_rpow_nnnorm two_ne_zero ENNReal.two_ne_top, not_lt, top_le_iff,
      ENNReal.toReal_ofNat, one_div, ENNReal.rpow_eq_top_iff, inv_lt_zero, inv_pos, and_true,
      or_iff_not_imp_left, not_and_or, zero_lt_two] at hâ„’
    exact mod_cast hâ„’ fun _ => zero_le_two

/-- **Chebyshev's inequality** for `â„â‰¥0âˆ`-valued variance. -/
theorem meas_ge_le_evariance_div_sq {X : Î© â†’ â„} (hX : AEStronglyMeasurable X Î¼) {c : â„â‰¥0}
    (hc : c â‰  0) : Î¼ {Ï‰ | â†‘c â‰¤ |X Ï‰ - Î¼[X]|} â‰¤ evariance X Î¼ / c ^ 2 := by
  have A : (c : â„â‰¥0âˆ) â‰  0 := by rwa [Ne, ENNReal.coe_eq_zero]
  have B : AEStronglyMeasurable (fun _ : Î© => Î¼[X]) Î¼ := aestronglyMeasurable_const
  convert meas_ge_le_mul_pow_eLpNorm Î¼ two_ne_zero ENNReal.two_ne_top (hX.sub B) A using 1
  Â· congr
    simp only [Pi.sub_apply, ENNReal.coe_le_coe, â† Real.norm_eq_abs, â† coe_nnnorm,
      NNReal.coe_le_coe, ENNReal.ofReal_coe_nnreal]
  Â· rw [eLpNorm_eq_lintegral_rpow_nnnorm two_ne_zero ENNReal.two_ne_top]
    simp only [show ENNReal.ofNNReal (c ^ 2) = (ENNReal.ofNNReal c) ^ 2 by norm_cast,
      ENNReal.toReal_ofNat, one_div, Pi.sub_apply]
    rw [div_eq_mul_inv, ENNReal.inv_pow, mul_comm, ENNReal.rpow_two]
    congr
    simp_rw [â† ENNReal.rpow_mul, inv_mul_cancelâ‚€ (two_ne_zero : (2 : â„) â‰  0), ENNReal.rpow_two,
      ENNReal.rpow_one, evariance]

/-- **Chebyshev's inequality**: one can control the deviation probability of a real random variable
from its expectation in terms of the variance. -/
theorem meas_ge_le_variance_div_sq [IsFiniteMeasure Î¼] {X : Î© â†’ â„} (hX : Memâ„’p X 2 Î¼) {c : â„}
    (hc : 0 < c) : Î¼ {Ï‰ | c â‰¤ |X Ï‰ - Î¼[X]|} â‰¤ ENNReal.ofReal (variance X Î¼ / c ^ 2) := by
  rw [ENNReal.ofReal_div_of_pos (sq_pos_of_ne_zero hc.ne.symm), hX.ofReal_variance_eq]
  convert @meas_ge_le_evariance_div_sq _ _ _ _ hX.1 c.toNNReal (by simp [hc]) using 1
  Â· simp only [Real.coe_toNNReal', max_le_iff, abs_nonneg, and_true]
  Â· rw [ENNReal.ofReal_pow hc.le]
    rfl

-- Porting note: supplied `MeasurableSpace Î©` argument of `h` by unification
/-- The variance of the sum of two independent random variables is the sum of the variances. -/
theorem IndepFun.variance_add [IsProbabilityMeasure Î¼] {X Y : Î© â†’ â„} (hX : Memâ„’p X 2 Î¼)
    (hY : Memâ„’p Y 2 Î¼) (h : IndepFun X Y Î¼) : variance (X + Y) Î¼ = variance X Î¼ + variance Y Î¼ :=
  calc
    variance (X + Y) Î¼ = Î¼[fun a => X a ^ 2 + Y a ^ 2 + 2 * X a * Y a] - Î¼[X + Y] ^ 2 := by
      simp [variance_def' (hX.add hY), add_sq']
    _ = Î¼[X ^ 2] + Î¼[Y ^ 2] + (2 : â„) * Î¼[X * Y] - (Î¼[X] + Î¼[Y]) ^ 2 := by
      simp only [Pi.add_apply, Pi.pow_apply, Pi.mul_apply, mul_assoc]
      rw [integral_add, integral_add, integral_add, integral_mul_left]
      Â· exact hX.integrable one_le_two
      Â· exact hY.integrable one_le_two
      Â· exact hX.integrable_sq
      Â· exact hY.integrable_sq
      Â· exact hX.integrable_sq.add hY.integrable_sq
      Â· apply Integrable.const_mul
        exact h.integrable_mul (hX.integrable one_le_two) (hY.integrable one_le_two)
    _ = Î¼[X ^ 2] + Î¼[Y ^ 2] + 2 * (Î¼[X] * Î¼[Y]) - (Î¼[X] + Î¼[Y]) ^ 2 := by
      congr
      exact h.integral_mul_of_integrable (hX.integrable one_le_two) (hY.integrable one_le_two)
    _ = variance X Î¼ + variance Y Î¼ := by simp only [variance_def', hX, hY, Pi.pow_apply]; ring

-- Porting note: supplied `MeasurableSpace Î©` argument of `hs`, `h` by unification
/-- The variance of a finite sum of pairwise independent random variables is the sum of the
variances. -/
theorem IndepFun.variance_sum [IsProbabilityMeasure Î¼] {Î¹ : Type*} {X : Î¹ â†’ Î© â†’ â„}
    {s : Finset Î¹} (hs : âˆ€ i âˆˆ s, Memâ„’p (X i) 2 Î¼)
    (h : Set.Pairwise â†‘s fun i j => IndepFun (X i) (X j) Î¼) :
    variance (âˆ‘ i âˆˆ s, X i) Î¼ = âˆ‘ i âˆˆ s, variance (X i) Î¼ := by
  classical
  induction' s using Finset.induction_on with k s ks IH
  Â· simp only [Finset.sum_empty, variance_zero]
  rw [variance_def' (memâ„’p_finset_sum' _ hs), sum_insert ks, sum_insert ks]
  simp only [add_sq']
  calc
    Î¼[(X k ^ 2 + (âˆ‘ i âˆˆ s, X i) ^ 2 + 2 * X k * âˆ‘ i âˆˆ s, X i : Î© â†’ â„)] - Î¼[X k + âˆ‘ i âˆˆ s, X i] ^ 2 =
        Î¼[X k ^ 2] + Î¼[(âˆ‘ i âˆˆ s, X i) ^ 2] + Î¼[2 * X k * âˆ‘ i âˆˆ s, X i] -
          (Î¼[X k] + Î¼[âˆ‘ i âˆˆ s, X i]) ^ 2 := by
      rw [integral_add', integral_add', integral_add']
      Â· exact Memâ„’p.integrable one_le_two (hs _ (mem_insert_self _ _))
      Â· apply integrable_finset_sum' _ fun i hi => ?_
        exact Memâ„’p.integrable one_le_two (hs _ (mem_insert_of_mem hi))
      Â· exact Memâ„’p.integrable_sq (hs _ (mem_insert_self _ _))
      Â· apply Memâ„’p.integrable_sq
        exact memâ„’p_finset_sum' _ fun i hi => hs _ (mem_insert_of_mem hi)
      Â· apply Integrable.add
        Â· exact Memâ„’p.integrable_sq (hs _ (mem_insert_self _ _))
        Â· apply Memâ„’p.integrable_sq
          exact memâ„’p_finset_sum' _ fun i hi => hs _ (mem_insert_of_mem hi)
      Â· rw [mul_assoc]
        apply Integrable.const_mul _ (2 : â„)
        rw [mul_sum, sum_fn]
        apply integrable_finset_sum _ fun i hi => ?_
        apply IndepFun.integrable_mul _ (Memâ„’p.integrable one_le_two (hs _ (mem_insert_self _ _)))
          (Memâ„’p.integrable one_le_two (hs _ (mem_insert_of_mem hi)))
        apply h (mem_insert_self _ _) (mem_insert_of_mem hi)
        exact fun hki => ks (hki.symm â–¸ hi)
    _ = variance (X k) Î¼ + variance (âˆ‘ i âˆˆ s, X i) Î¼ +
        (Î¼[2 * X k * âˆ‘ i âˆˆ s, X i] - 2 * Î¼[X k] * Î¼[âˆ‘ i âˆˆ s, X i]) := by
      rw [variance_def' (hs _ (mem_insert_self _ _)),
        variance_def' (memâ„’p_finset_sum' _ fun i hi => hs _ (mem_insert_of_mem hi))]
      ring
    _ = variance (X k) Î¼ + variance (âˆ‘ i âˆˆ s, X i) Î¼ := by
      simp_rw [Pi.mul_apply, Pi.ofNat_apply, Nat.cast_ofNat, sum_apply, mul_sum, mul_assoc,
        add_right_eq_self]
      rw [integral_finset_sum s fun i hi => ?_]; swap
      Â· apply Integrable.const_mul _ (2 : â„)
        apply IndepFun.integrable_mul _ (Memâ„’p.integrable one_le_two (hs _ (mem_insert_self _ _)))
          (Memâ„’p.integrable one_le_two (hs _ (mem_insert_of_mem hi)))
        apply h (mem_insert_self _ _) (mem_insert_of_mem hi)
        exact fun hki => ks (hki.symm â–¸ hi)
      rw [integral_finset_sum s fun i hi =>
          Memâ„’p.integrable one_le_two (hs _ (mem_insert_of_mem hi)),
        mul_sum, mul_sum, â† sum_sub_distrib]
      apply Finset.sum_eq_zero fun i hi => ?_
      rw [integral_mul_left, IndepFun.integral_mul', sub_self]
      Â· apply h (mem_insert_self _ _) (mem_insert_of_mem hi)
        exact fun hki => ks (hki.symm â–¸ hi)
      Â· exact Memâ„’p.aestronglyMeasurable (hs _ (mem_insert_self _ _))
      Â· exact Memâ„’p.aestronglyMeasurable (hs _ (mem_insert_of_mem hi))
    _ = variance (X k) Î¼ + âˆ‘ i âˆˆ s, variance (X i) Î¼ := by
      rw [IH (fun i hi => hs i (mem_insert_of_mem hi))
          (h.mono (by simp only [coe_insert, Set.subset_insert]))]

/-- **The Bhatia-Davis inequality on variance**

The variance of a random variable `X` satisfying `a â‰¤ X â‰¤ b` almost everywhere is at most
`(b - ğ”¼ X) * (ğ”¼ X - a)`. -/
lemma variance_le_sub_mul_sub [IsProbabilityMeasure Î¼] {a b : â„} {X : Î© â†’ â„}
    (h : âˆ€áµ Ï‰ âˆ‚Î¼, X Ï‰ âˆˆ Set.Icc a b) (hX : AEMeasurable X Î¼) :
    variance X Î¼ â‰¤ (b - Î¼[X]) * (Î¼[X] - a) := by
  have ha : âˆ€áµ Ï‰ âˆ‚Î¼, a â‰¤ X Ï‰ := h.mono fun Ï‰ h => h.1
  have hb : âˆ€áµ Ï‰ âˆ‚Î¼, X Ï‰ â‰¤ b := h.mono fun Ï‰ h => h.2
  have hX_intâ‚‚ : Integrable (fun Ï‰ â†¦ -X Ï‰ ^ 2) Î¼ :=
    (memâ„’p_of_bounded h hX.aestronglyMeasurable 2).integrable_sq.neg
  have hX_intâ‚ : Integrable (fun Ï‰ â†¦ (a + b) * X Ï‰) Î¼ :=
    ((integrable_const (max |a| |b|)).mono' hX.aestronglyMeasurable
      (by filter_upwards [ha, hb] with Ï‰ using abs_le_max_abs_abs)).const_mul (a + b)
  have h0 : 0 â‰¤ - Î¼[X ^ 2] + (a + b) * Î¼[X] - a * b :=
    calc
      _ â‰¤ âˆ« Ï‰, (b - X Ï‰) * (X Ï‰ - a) âˆ‚Î¼ := by
        apply integral_nonneg_of_ae
        filter_upwards [ha, hb] with Ï‰ ha' hb'
        exact mul_nonneg (by linarith : 0 â‰¤ b - X Ï‰) (by linarith : 0 â‰¤ X Ï‰ - a)
      _ = âˆ« Ï‰, - X Ï‰ ^ 2 + (a + b) * X Ï‰ - a * b âˆ‚Î¼ :=
        integral_congr_ae <| ae_of_all Î¼ fun Ï‰ â†¦ by ring
      _ = âˆ« Ï‰, - X Ï‰ ^ 2 + (a + b) * X Ï‰ âˆ‚Î¼ - âˆ« _, a * b âˆ‚Î¼ :=
        integral_sub (hX_intâ‚‚.add hX_intâ‚) (integrable_const (a * b))
      _ = âˆ« Ï‰, - X Ï‰ ^ 2 + (a + b) * X Ï‰ âˆ‚Î¼ - a * b := by simp
      _ = - Î¼[X ^ 2] + (a + b) * Î¼[X] - a * b := by
        simp [â† integral_neg, â† integral_mul_left, integral_add hX_intâ‚‚ hX_intâ‚]
  calc
    _ â‰¤ (a + b) * Î¼[X] - a * b - Î¼[X] ^ 2 := by
      rw [variance_def' (memâ„’p_of_bounded h hX.aestronglyMeasurable 2)]
      linarith
    _ = (b - Î¼[X]) * (Î¼[X] - a) := by ring

/-- **Popoviciu's inequality on variance**

The variance of a random variable `X` satisfying `a â‰¤ X â‰¤ b` almost everywhere is at most
`((b - a) / 2) ^ 2`. -/
lemma variance_le_sq_of_bounded [IsProbabilityMeasure Î¼] {a b : â„} {X : Î© â†’ â„}
    (h : âˆ€áµ Ï‰ âˆ‚Î¼, X Ï‰ âˆˆ Set.Icc a b) (hX : AEMeasurable X Î¼) :
    variance X Î¼ â‰¤ ((b - a) / 2) ^ 2 :=
  calc
    _ â‰¤ (b - Î¼[X]) * (Î¼[X] - a) := variance_le_sub_mul_sub h hX
    _ = ((b - a) / 2) ^ 2 - (Î¼[X] - (b + a) / 2) ^ 2 := by ring
    _ â‰¤ ((b - a) / 2) ^ 2 := sub_le_self _ (sq_nonneg _)

end ProbabilityTheory
