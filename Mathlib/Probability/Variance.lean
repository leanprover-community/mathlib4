/-
Copyright (c) 2022 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Kexing Ying
-/
import Mathlib.Probability.Notation
import Mathlib.Probability.Integration
import Mathlib.MeasureTheory.Function.L2Space

#align_import probability.variance from "leanprover-community/mathlib"@"f0c8bf9245297a541f468be517f1bde6195105e9"

/-!
# Variance of random variables

We define the variance of a real-valued random variable as `Var[X] = ùîº[(X - ùîº[X])^2]` (in the
`ProbabilityTheory` locale).

## Main definitions

* `ProbabilityTheory.evariance`: the variance of a real-valued random variable as an extended
  non-negative real.
* `ProbabilityTheory.variance`: the variance of a real-valued random variable as a real number.

## Main results

* `ProbabilityTheory.variance_le_expectation_sq`: the inequality `Var[X] ‚â§ ùîº[X^2]`.
* `ProbabilityTheory.meas_ge_le_variance_div_sq`: Chebyshev's inequality, i.e.,
      `‚Ñô {œâ | c ‚â§ |X œâ - ùîº[X]|} ‚â§ ENNReal.ofReal (Var[X] / c ^ 2)`.
* `ProbabilityTheory.meas_ge_le_evariance_div_sq`: Chebyshev's inequality formulated with
  `evariance` without requiring the random variables to be L¬≤.
* `ProbabilityTheory.IndepFun.variance_add`: the variance of the sum of two independent
  random variables is the sum of the variances.
* `ProbabilityTheory.IndepFun.variance_sum`: the variance of a finite sum of pairwise
  independent random variables is the sum of the variances.
-/


open MeasureTheory Filter Finset

noncomputable section

open scoped MeasureTheory ProbabilityTheory ENNReal NNReal

namespace ProbabilityTheory

-- Porting note: this lemma replaces `ENNReal.toReal_bit0`, which does not exist in Lean 4
private lemma coe_two : ENNReal.toReal 2 = (2 : ‚Ñù) := rfl

-- Porting note: Consider if `evariance` or `eVariance` is better. Also,
-- consider `eVariationOn` in `Mathlib.Analysis.BoundedVariation`.
/-- The `‚Ñù‚â•0‚àû`-valued variance of a real-valued random variable defined as the Lebesgue integral of
`(X - ùîº[X])^2`. -/
def evariance {Œ© : Type*} {_ : MeasurableSpace Œ©} (X : Œ© ‚Üí ‚Ñù) (Œº : Measure Œ©) : ‚Ñù‚â•0‚àû :=
  ‚à´‚Åª œâ, (‚ÄñX œâ - Œº[X]‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ 2 ‚àÇŒº
#align probability_theory.evariance ProbabilityTheory.evariance

/-- The `‚Ñù`-valued variance of a real-valued random variable defined by applying `ENNReal.toReal`
to `evariance`. -/
def variance {Œ© : Type*} {_ : MeasurableSpace Œ©} (X : Œ© ‚Üí ‚Ñù) (Œº : Measure Œ©) : ‚Ñù :=
  (evariance X Œº).toReal
#align probability_theory.variance ProbabilityTheory.variance

variable {Œ© : Type*} {m : MeasurableSpace Œ©} {X : Œ© ‚Üí ‚Ñù} {Œº : Measure Œ©}

theorem _root_.MeasureTheory.Mem‚Ñíp.evariance_lt_top [IsFiniteMeasure Œº] (hX : Mem‚Ñíp X 2 Œº) :
    evariance X Œº < ‚àû := by
  have := ENNReal.pow_lt_top (hX.sub <| mem‚Ñíp_const <| Œº[X]).2 2
  rw [snorm_eq_lintegral_rpow_nnnorm two_ne_zero ENNReal.two_ne_top, ‚Üê ENNReal.rpow_two] at this
  simp only [coe_two, Pi.sub_apply, ENNReal.one_toReal, one_div] at this
  rw [‚Üê ENNReal.rpow_mul, inv_mul_cancel (two_ne_zero : (2 : ‚Ñù) ‚â† 0), ENNReal.rpow_one] at this
  simp_rw [ENNReal.rpow_two] at this
  exact this
#align measure_theory.mem_‚Ñíp.evariance_lt_top MeasureTheory.Mem‚Ñíp.evariance_lt_top

theorem evariance_eq_top [IsFiniteMeasure Œº] (hXm : AEStronglyMeasurable X Œº) (hX : ¬¨Mem‚Ñíp X 2 Œº) :
    evariance X Œº = ‚àû := by
  by_contra h
  rw [‚Üê Ne, ‚Üê lt_top_iff_ne_top] at h
  have : Mem‚Ñíp (fun œâ => X œâ - Œº[X]) 2 Œº := by
    refine ‚ü®hXm.sub aestronglyMeasurable_const, ?_‚ü©
    rw [snorm_eq_lintegral_rpow_nnnorm two_ne_zero ENNReal.two_ne_top]
    simp only [coe_two, ENNReal.one_toReal, ENNReal.rpow_two, Ne]
    exact ENNReal.rpow_lt_top_of_nonneg (by linarith) h.ne
  refine hX ?_
  -- Porting note: `Œº[X]` without whitespace is ambiguous as it could be GetElem,
  -- and `convert` cannot disambiguate based on typeclass inference failure.
  convert this.add (mem‚Ñíp_const <| Œº [X])
  ext œâ
  rw [Pi.add_apply, sub_add_cancel]
#align probability_theory.evariance_eq_top ProbabilityTheory.evariance_eq_top

theorem evariance_lt_top_iff_mem‚Ñíp [IsFiniteMeasure Œº] (hX : AEStronglyMeasurable X Œº) :
    evariance X Œº < ‚àû ‚Üî Mem‚Ñíp X 2 Œº := by
  refine ‚ü®?_, MeasureTheory.Mem‚Ñíp.evariance_lt_top‚ü©
  contrapose
  rw [not_lt, top_le_iff]
  exact evariance_eq_top hX
#align probability_theory.evariance_lt_top_iff_mem_‚Ñíp ProbabilityTheory.evariance_lt_top_iff_mem‚Ñíp

theorem _root_.MeasureTheory.Mem‚Ñíp.ofReal_variance_eq [IsFiniteMeasure Œº] (hX : Mem‚Ñíp X 2 Œº) :
    ENNReal.ofReal (variance X Œº) = evariance X Œº := by
  rw [variance, ENNReal.ofReal_toReal]
  exact hX.evariance_lt_top.ne
#align measure_theory.mem_‚Ñíp.of_real_variance_eq MeasureTheory.Mem‚Ñíp.ofReal_variance_eq

theorem evariance_eq_lintegral_ofReal (X : Œ© ‚Üí ‚Ñù) (Œº : Measure Œ©) :
    evariance X Œº = ‚à´‚Åª œâ, ENNReal.ofReal ((X œâ - Œº[X]) ^ 2) ‚àÇŒº := by
  rw [evariance]
  congr
  ext1 œâ
  rw [pow_two, ‚Üê ENNReal.coe_mul, ‚Üê nnnorm_mul, ‚Üê pow_two]
  congr
  exact (Real.toNNReal_eq_nnnorm_of_nonneg <| sq_nonneg _).symm
#align probability_theory.evariance_eq_lintegral_of_real ProbabilityTheory.evariance_eq_lintegral_ofReal

theorem _root_.MeasureTheory.Mem‚Ñíp.variance_eq_of_integral_eq_zero (hX : Mem‚Ñíp X 2 Œº)
    (hXint : Œº[X] = 0) : variance X Œº = Œº[X ^ (2 : Nat)] := by
  rw [variance, evariance_eq_lintegral_ofReal, ‚Üê ofReal_integral_eq_lintegral_ofReal,
      ENNReal.toReal_ofReal (by positivity)] <;>
    simp_rw [hXint, sub_zero]
  ¬∑ rfl
  ¬∑ convert hX.integrable_norm_rpow two_ne_zero ENNReal.two_ne_top with œâ
    simp only [Pi.sub_apply, Real.norm_eq_abs, coe_two, ENNReal.one_toReal,
      Real.rpow_two, sq_abs, abs_pow]
  ¬∑ exact ae_of_all _ fun œâ => pow_two_nonneg _
#align measure_theory.mem_‚Ñíp.variance_eq_of_integral_eq_zero MeasureTheory.Mem‚Ñíp.variance_eq_of_integral_eq_zero

theorem _root_.MeasureTheory.Mem‚Ñíp.variance_eq [IsFiniteMeasure Œº] (hX : Mem‚Ñíp X 2 Œº) :
    variance X Œº = Œº[(X - fun _ => Œº[X] :) ^ (2 : Nat)] := by
  rw [variance, evariance_eq_lintegral_ofReal, ‚Üê ofReal_integral_eq_lintegral_ofReal,
    ENNReal.toReal_ofReal (by positivity)]
  ¬∑ rfl
  ¬∑ -- Porting note: `Œº[X]` without whitespace is ambiguous as it could be GetElem,
    -- and `convert` cannot disambiguate based on typeclass inference failure.
    convert (hX.sub <| mem‚Ñíp_const (Œº [X])).integrable_norm_rpow two_ne_zero ENNReal.two_ne_top
      with œâ
    simp only [Pi.sub_apply, Real.norm_eq_abs, coe_two, ENNReal.one_toReal,
      Real.rpow_two, sq_abs, abs_pow]
  ¬∑ exact ae_of_all _ fun œâ => pow_two_nonneg _
#align measure_theory.mem_‚Ñíp.variance_eq MeasureTheory.Mem‚Ñíp.variance_eq

@[simp]
theorem evariance_zero : evariance 0 Œº = 0 := by simp [evariance]
#align probability_theory.evariance_zero ProbabilityTheory.evariance_zero

theorem evariance_eq_zero_iff (hX : AEMeasurable X Œº) :
    evariance X Œº = 0 ‚Üî X =·µê[Œº] fun _ => Œº[X] := by
  rw [evariance, lintegral_eq_zero_iff']
  constructor <;> intro hX <;> filter_upwards [hX] with œâ hœâ
  ¬∑ simpa only [Pi.zero_apply, sq_eq_zero_iff, ENNReal.coe_eq_zero, nnnorm_eq_zero, sub_eq_zero]
      using hœâ
  ¬∑ rw [hœâ]
    simp
  ¬∑ exact (hX.sub_const _).ennnorm.pow_const _ -- TODO `measurability` and `fun_prop` fail
#align probability_theory.evariance_eq_zero_iff ProbabilityTheory.evariance_eq_zero_iff

theorem evariance_mul (c : ‚Ñù) (X : Œ© ‚Üí ‚Ñù) (Œº : Measure Œ©) :
    evariance (fun œâ => c * X œâ) Œº = ENNReal.ofReal (c ^ 2) * evariance X Œº := by
  rw [evariance, evariance, ‚Üê lintegral_const_mul' _ _ ENNReal.ofReal_lt_top.ne]
  congr
  ext1 œâ
  rw [ENNReal.ofReal, ‚Üê ENNReal.coe_pow, ‚Üê ENNReal.coe_pow, ‚Üê ENNReal.coe_mul]
  congr
  rw [‚Üê sq_abs, ‚Üê Real.rpow_two, Real.toNNReal_rpow_of_nonneg (abs_nonneg _), NNReal.rpow_two,
    ‚Üê mul_pow, Real.toNNReal_mul_nnnorm _ (abs_nonneg _)]
  conv_rhs => rw [‚Üê nnnorm_norm, norm_mul, norm_abs_eq_norm, ‚Üê norm_mul, nnnorm_norm, mul_sub]
  congr
  rw [mul_comm]
  simp_rw [‚Üê smul_eq_mul, ‚Üê integral_smul_const, smul_eq_mul, mul_comm]
#align probability_theory.evariance_mul ProbabilityTheory.evariance_mul

scoped notation "eVar[" X "]" => ProbabilityTheory.evariance X MeasureTheory.MeasureSpace.volume

@[simp]
theorem variance_zero (Œº : Measure Œ©) : variance 0 Œº = 0 := by
  simp only [variance, evariance_zero, ENNReal.zero_toReal]
#align probability_theory.variance_zero ProbabilityTheory.variance_zero

theorem variance_nonneg (X : Œ© ‚Üí ‚Ñù) (Œº : Measure Œ©) : 0 ‚â§ variance X Œº :=
  ENNReal.toReal_nonneg
#align probability_theory.variance_nonneg ProbabilityTheory.variance_nonneg

theorem variance_mul (c : ‚Ñù) (X : Œ© ‚Üí ‚Ñù) (Œº : Measure Œ©) :
    variance (fun œâ => c * X œâ) Œº = c ^ 2 * variance X Œº := by
  rw [variance, evariance_mul, ENNReal.toReal_mul, ENNReal.toReal_ofReal (sq_nonneg _)]
  rfl
#align probability_theory.variance_mul ProbabilityTheory.variance_mul

theorem variance_smul (c : ‚Ñù) (X : Œ© ‚Üí ‚Ñù) (Œº : Measure Œ©) :
    variance (c ‚Ä¢ X) Œº = c ^ 2 * variance X Œº :=
  variance_mul c X Œº
#align probability_theory.variance_smul ProbabilityTheory.variance_smul

theorem variance_smul' {A : Type*} [CommSemiring A] [Algebra A ‚Ñù] (c : A) (X : Œ© ‚Üí ‚Ñù)
    (Œº : Measure Œ©) : variance (c ‚Ä¢ X) Œº = c ^ 2 ‚Ä¢ variance X Œº := by
  convert variance_smul (algebraMap A ‚Ñù c) X Œº using 1
  ¬∑ congr; simp only [algebraMap_smul]
  ¬∑ simp only [Algebra.smul_def, map_pow]
#align probability_theory.variance_smul' ProbabilityTheory.variance_smul'

scoped notation "Var[" X "]" => ProbabilityTheory.variance X MeasureTheory.MeasureSpace.volume

variable [MeasureSpace Œ©]

theorem variance_def' [@IsProbabilityMeasure Œ© _ ‚Ñô] {X : Œ© ‚Üí ‚Ñù} (hX : Mem‚Ñíp X 2) :
    Var[X] = ùîº[X ^ 2] - ùîº[X] ^ 2 := by
  rw [hX.variance_eq, sub_sq', integral_sub', integral_add']; rotate_left
  ¬∑ exact hX.integrable_sq
  ¬∑ convert @integrable_const Œ© ‚Ñù (_) ‚Ñô _ _ (ùîº[X] ^ 2)
  ¬∑ apply hX.integrable_sq.add
    convert @integrable_const Œ© ‚Ñù (_) ‚Ñô _ _ (ùîº[X] ^ 2)
  ¬∑ exact ((hX.integrable one_le_two).const_mul 2).mul_const' _
  simp only [Pi.pow_apply, integral_const, measure_univ, ENNReal.one_toReal, smul_eq_mul, one_mul,
    Pi.mul_apply, Pi.ofNat_apply, Nat.cast_ofNat, integral_mul_right, integral_mul_left]
  ring
#align probability_theory.variance_def' ProbabilityTheory.variance_def'

theorem variance_le_expectation_sq [@IsProbabilityMeasure Œ© _ ‚Ñô] {X : Œ© ‚Üí ‚Ñù}
    (hm : AEStronglyMeasurable X ‚Ñô) : Var[X] ‚â§ ùîº[X ^ 2] := by
  by_cases hX : Mem‚Ñíp X 2
  ¬∑ rw [variance_def' hX]
    simp only [sq_nonneg, sub_le_self_iff]
  rw [variance, evariance_eq_lintegral_ofReal, ‚Üê integral_eq_lintegral_of_nonneg_ae]
  ¬∑ by_cases hint : Integrable X; swap
    ¬∑ simp only [integral_undef hint, Pi.pow_apply, Pi.sub_apply, sub_zero]
      exact le_rfl
    ¬∑ rw [integral_undef]
      ¬∑ exact integral_nonneg fun a => sq_nonneg _
      intro h
      have A : Mem‚Ñíp (X - fun œâ : Œ© => ùîº[X]) 2 ‚Ñô :=
        (mem‚Ñíp_two_iff_integrable_sq (hint.aestronglyMeasurable.sub aestronglyMeasurable_const)).2 h
      have B : Mem‚Ñíp (fun _ : Œ© => ùîº[X]) 2 ‚Ñô := mem‚Ñíp_const _
      apply hX
      convert A.add B
      simp
  ¬∑ exact eventually_of_forall fun x => sq_nonneg _
  ¬∑ exact (AEMeasurable.pow_const (hm.aemeasurable.sub_const _) _).aestronglyMeasurable
#align probability_theory.variance_le_expectation_sq ProbabilityTheory.variance_le_expectation_sq

theorem evariance_def' [@IsProbabilityMeasure Œ© _ ‚Ñô] {X : Œ© ‚Üí ‚Ñù} (hX : AEStronglyMeasurable X ‚Ñô) :
    eVar[X] = (‚à´‚Åª œâ, (‚ÄñX œâ‚Äñ‚Çä ^ 2 :)) - ENNReal.ofReal (ùîº[X] ^ 2) := by
  by_cases h‚Ñí : Mem‚Ñíp X 2
  ¬∑ rw [‚Üê h‚Ñí.ofReal_variance_eq, variance_def' h‚Ñí, ENNReal.ofReal_sub _ (sq_nonneg _)]
    congr
    rw [lintegral_coe_eq_integral]
    ¬∑ congr 2 with œâ
      simp only [Pi.pow_apply, NNReal.coe_pow, coe_nnnorm, Real.norm_eq_abs, Even.pow_abs even_two]
    ¬∑ exact h‚Ñí.abs.integrable_sq
  ¬∑ symm
    rw [evariance_eq_top hX h‚Ñí, ENNReal.sub_eq_top_iff]
    refine ‚ü®?_, ENNReal.ofReal_ne_top‚ü©
    rw [Mem‚Ñíp, not_and] at h‚Ñí
    specialize h‚Ñí hX
    simp only [snorm_eq_lintegral_rpow_nnnorm two_ne_zero ENNReal.two_ne_top, not_lt, top_le_iff,
      coe_two, one_div, ENNReal.rpow_eq_top_iff, inv_lt_zero, inv_pos, and_true_iff,
      or_iff_not_imp_left, not_and_or, zero_lt_two] at h‚Ñí
    exact mod_cast h‚Ñí fun _ => zero_le_two
#align probability_theory.evariance_def' ProbabilityTheory.evariance_def'

/-- **Chebyshev's inequality** for `‚Ñù‚â•0‚àû`-valued variance. -/
theorem meas_ge_le_evariance_div_sq {X : Œ© ‚Üí ‚Ñù} (hX : AEStronglyMeasurable X ‚Ñô) {c : ‚Ñù‚â•0}
    (hc : c ‚â† 0) : ‚Ñô {œâ | ‚Üëc ‚â§ |X œâ - ùîº[X]|} ‚â§ eVar[X] / c ^ 2 := by
  have A : (c : ‚Ñù‚â•0‚àû) ‚â† 0 := by rwa [Ne, ENNReal.coe_eq_zero]
  have B : AEStronglyMeasurable (fun _ : Œ© => ùîº[X]) ‚Ñô := aestronglyMeasurable_const
  convert meas_ge_le_mul_pow_snorm ‚Ñô two_ne_zero ENNReal.two_ne_top (hX.sub B) A using 1
  ¬∑ congr
    simp only [Pi.sub_apply, ENNReal.coe_le_coe, ‚Üê Real.norm_eq_abs, ‚Üê coe_nnnorm,
      NNReal.coe_le_coe, ENNReal.ofReal_coe_nnreal]
  ¬∑ rw [snorm_eq_lintegral_rpow_nnnorm two_ne_zero ENNReal.two_ne_top]
    simp only [show ENNReal.ofNNReal (c ^ 2) = (ENNReal.ofNNReal c) ^ 2 by norm_cast, coe_two,
      one_div, Pi.sub_apply]
    rw [div_eq_mul_inv, ENNReal.inv_pow, mul_comm, ENNReal.rpow_two]
    congr
    simp_rw [‚Üê ENNReal.rpow_mul, inv_mul_cancel (two_ne_zero : (2 : ‚Ñù) ‚â† 0), ENNReal.rpow_two,
      ENNReal.rpow_one, evariance]
#align probability_theory.meas_ge_le_evariance_div_sq ProbabilityTheory.meas_ge_le_evariance_div_sq

/-- **Chebyshev's inequality**: one can control the deviation probability of a real random variable
from its expectation in terms of the variance. -/
theorem meas_ge_le_variance_div_sq [@IsFiniteMeasure Œ© _ ‚Ñô] {X : Œ© ‚Üí ‚Ñù} (hX : Mem‚Ñíp X 2) {c : ‚Ñù}
    (hc : 0 < c) : ‚Ñô {œâ | c ‚â§ |X œâ - ùîº[X]|} ‚â§ ENNReal.ofReal (Var[X] / c ^ 2) := by
  rw [ENNReal.ofReal_div_of_pos (sq_pos_of_ne_zero hc.ne.symm), hX.ofReal_variance_eq]
  convert @meas_ge_le_evariance_div_sq _ _ _ hX.1 c.toNNReal (by simp [hc]) using 1
  ¬∑ simp only [Real.coe_toNNReal', max_le_iff, abs_nonneg, and_true_iff]
  ¬∑ rw [ENNReal.ofReal_pow hc.le]
    rfl
#align probability_theory.meas_ge_le_variance_div_sq ProbabilityTheory.meas_ge_le_variance_div_sq

-- Porting note: supplied `MeasurableSpace Œ©` argument of `h` by unification
/-- The variance of the sum of two independent random variables is the sum of the variances. -/
theorem IndepFun.variance_add [@IsProbabilityMeasure Œ© _ ‚Ñô] {X Y : Œ© ‚Üí ‚Ñù} (hX : Mem‚Ñíp X 2)
    (hY : Mem‚Ñíp Y 2) (h : @IndepFun _ _ _ (_) _ _ X Y ‚Ñô) : Var[X + Y] = Var[X] + Var[Y] :=
  calc
    Var[X + Y] = ùîº[fun a => X a ^ 2 + Y a ^ 2 + 2 * X a * Y a] - ùîº[X + Y] ^ 2 := by
      simp [variance_def' (hX.add hY), add_sq']
    _ = ùîº[X ^ 2] + ùîº[Y ^ 2] + (2 : ‚Ñù) * ùîº[X * Y] - (ùîº[X] + ùîº[Y]) ^ 2 := by
      simp only [Pi.add_apply, Pi.pow_apply, Pi.mul_apply, mul_assoc]
      rw [integral_add, integral_add, integral_add, integral_mul_left]
      ¬∑ exact hX.integrable one_le_two
      ¬∑ exact hY.integrable one_le_two
      ¬∑ exact hX.integrable_sq
      ¬∑ exact hY.integrable_sq
      ¬∑ exact hX.integrable_sq.add hY.integrable_sq
      ¬∑ apply Integrable.const_mul
        exact h.integrable_mul (hX.integrable one_le_two) (hY.integrable one_le_two)
    _ = ùîº[X ^ 2] + ùîº[Y ^ 2] + 2 * (ùîº[X] * ùîº[Y]) - (ùîº[X] + ùîº[Y]) ^ 2 := by
      congr
      exact h.integral_mul_of_integrable (hX.integrable one_le_two) (hY.integrable one_le_two)
    _ = Var[X] + Var[Y] := by simp only [variance_def', hX, hY, Pi.pow_apply]; ring
#align probability_theory.indep_fun.variance_add ProbabilityTheory.IndepFun.variance_add

-- Porting note: supplied `MeasurableSpace Œ©` argument of `hs`, `h` by unification
/-- The variance of a finite sum of pairwise independent random variables is the sum of the
variances. -/
theorem IndepFun.variance_sum [@IsProbabilityMeasure Œ© _ ‚Ñô] {Œπ : Type*} {X : Œπ ‚Üí Œ© ‚Üí ‚Ñù}
    {s : Finset Œπ} (hs : ‚àÄ i ‚àà s, @Mem‚Ñíp _ _ _ (_) (X i) 2 ‚Ñô)
    (h : Set.Pairwise ‚Üës fun i j => @IndepFun _ _ _ (_) _ _ (X i) (X j) ‚Ñô) :
    Var[‚àë i ‚àà s, X i] = ‚àë i ‚àà s, Var[X i] := by
  classical
  induction' s using Finset.induction_on with k s ks IH
  ¬∑ simp only [Finset.sum_empty, variance_zero]
  rw [variance_def' (mem‚Ñíp_finset_sum' _ hs), sum_insert ks, sum_insert ks]
  simp only [add_sq']
  calc
    ùîº[X k ^ 2 + (‚àë i ‚àà s, X i) ^ 2 + 2 * X k * ‚àë i ‚àà s, X i] - ùîº[X k + ‚àë i ‚àà s, X i] ^ 2 =
        ùîº[X k ^ 2] + ùîº[(‚àë i ‚àà s, X i) ^ 2] + ùîº[2 * X k * ‚àë i ‚àà s, X i] -
          (ùîº[X k] + ùîº[‚àë i ‚àà s, X i]) ^ 2 := by
      rw [integral_add', integral_add', integral_add']
      ¬∑ exact Mem‚Ñíp.integrable one_le_two (hs _ (mem_insert_self _ _))
      ¬∑ apply integrable_finset_sum' _ fun i hi => ?_
        exact Mem‚Ñíp.integrable one_le_two (hs _ (mem_insert_of_mem hi))
      ¬∑ exact Mem‚Ñíp.integrable_sq (hs _ (mem_insert_self _ _))
      ¬∑ apply Mem‚Ñíp.integrable_sq
        exact mem‚Ñíp_finset_sum' _ fun i hi => hs _ (mem_insert_of_mem hi)
      ¬∑ apply Integrable.add
        ¬∑ exact Mem‚Ñíp.integrable_sq (hs _ (mem_insert_self _ _))
        ¬∑ apply Mem‚Ñíp.integrable_sq
          exact mem‚Ñíp_finset_sum' _ fun i hi => hs _ (mem_insert_of_mem hi)
      ¬∑ rw [mul_assoc]
        apply Integrable.const_mul _ (2 : ‚Ñù)
        simp only [mul_sum, sum_apply, Pi.mul_apply]
        apply integrable_finset_sum _ fun i hi => ?_
        apply IndepFun.integrable_mul _ (Mem‚Ñíp.integrable one_le_two (hs _ (mem_insert_self _ _)))
          (Mem‚Ñíp.integrable one_le_two (hs _ (mem_insert_of_mem hi)))
        apply h (mem_insert_self _ _) (mem_insert_of_mem hi)
        exact fun hki => ks (hki.symm ‚ñ∏ hi)
    _ = Var[X k] + Var[‚àë i ‚àà s, X i] +
        (ùîº[2 * X k * ‚àë i ‚àà s, X i] - 2 * ùîº[X k] * ùîº[‚àë i ‚àà s, X i]) := by
      rw [variance_def' (hs _ (mem_insert_self _ _)),
        variance_def' (mem‚Ñíp_finset_sum' _ fun i hi => hs _ (mem_insert_of_mem hi))]
      ring
    _ = Var[X k] + Var[‚àë i ‚àà s, X i] := by
      simp_rw [Pi.mul_apply, Pi.ofNat_apply, Nat.cast_ofNat, sum_apply, mul_sum, mul_assoc,
        add_right_eq_self]
      rw [integral_finset_sum s fun i hi => ?_]; swap
      ¬∑ apply Integrable.const_mul _ (2 : ‚Ñù)
        apply IndepFun.integrable_mul _ (Mem‚Ñíp.integrable one_le_two (hs _ (mem_insert_self _ _)))
          (Mem‚Ñíp.integrable one_le_two (hs _ (mem_insert_of_mem hi)))
        apply h (mem_insert_self _ _) (mem_insert_of_mem hi)
        exact fun hki => ks (hki.symm ‚ñ∏ hi)
      rw [integral_finset_sum s fun i hi =>
          Mem‚Ñíp.integrable one_le_two (hs _ (mem_insert_of_mem hi)),
        mul_sum, mul_sum, ‚Üê sum_sub_distrib]
      apply Finset.sum_eq_zero fun i hi => ?_
      rw [integral_mul_left, IndepFun.integral_mul', sub_self]
      ¬∑ apply h (mem_insert_self _ _) (mem_insert_of_mem hi)
        exact fun hki => ks (hki.symm ‚ñ∏ hi)
      ¬∑ exact Mem‚Ñíp.aestronglyMeasurable (hs _ (mem_insert_self _ _))
      ¬∑ exact Mem‚Ñíp.aestronglyMeasurable (hs _ (mem_insert_of_mem hi))
    _ = Var[X k] + ‚àë i ‚àà s, Var[X i] := by
      rw [IH (fun i hi => hs i (mem_insert_of_mem hi))
          (h.mono (by simp only [coe_insert, Set.subset_insert]))]
#align probability_theory.indep_fun.variance_sum ProbabilityTheory.IndepFun.variance_sum

end ProbabilityTheory
