/-
Copyright (c) 2023 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
module

public import Mathlib.Data.Nat.Choose.Multinomial
public import Mathlib.Probability.IdentDistrib

/-!
# The Marcinkiewicz-Zygmund inequality

This file proves the Marcinkiewicz-Zygmund inequality.

The Marcinkiewicz-Zygmund inequality states that, if `Xâ‚, ... Xâ‚ âˆˆ L^p` are independent random
variables of mean zero valued in some inner product space, then the `L^p`-norm of `Xâ‚ + ... + Xâ‚` is
at most `Câ‚š` times the `L^(p/2)`-norm of `|Xâ‚|Â² + ... + |Xâ‚|Â²`, where `Câ‚š` is a constant depending
on `p` alone.

## Notation

Throughout this file, `A ^^ n` denotes `A Ã— ... Ã— A` (with `n` factors). Formally, this is
`Fintype.piFinset fun _ : Fin n â†¦ A`.

## TODO

We currently only prove the inequality for `p = 2 * m` an even natural number. The general `p` case
can be obtained from this specific one by nesting of Lp norms.
-/

open Finset Fintype Function Nat MeasureTheory ProbabilityTheory Real
open scoped NNReal ENNReal

variable {Î¹ Î© E : Type*} {A : Finset Î¹} {m n : â„•} [MeasurableSpace Î©] {Î¼ : Measure Î©}
  [IsFiniteMeasure Î¼] [mE : MeasurableSpace E] [NormedAddCommGroup E] [InnerProductSpace â„ E]
  {X : Î¹ â†’ Î© â†’ E}

local notation:70 A:70 " ^^ " n:71 => Fintype.piFinset fun _ : Fin n â†¦ A

/-- The constant appearing in the Marcinkiewicz-Zygmund inequality for symmetric random variables.
-/
noncomputable def marcinkiewiczZygmundSymmConst (p : â„â‰¥0) : â„ := (p / 2) ^ (p / 2 : â„)

/-- The **Marcinkiewicz-Zygmund inequality** for symmetric random variables, with a slightly better
constant than `marcinkiewicz_zygmund`. -/
theorem marcinkiewicz_zygmund_symmetric (iIndepFun_X : iIndepFun X Î¼)
    (identDistrib_neg_X : âˆ€ i, IdentDistrib (X i) (-X i) Î¼ Î¼)
    (memLp_X : âˆ€ i âˆˆ A, MemLp (X i) (2 * m) Î¼) :
    âˆ« Ï‰, â€–âˆ‘ i âˆˆ A, X i Ï‰â€– ^ (2 * m) âˆ‚Î¼ â‰¤
      marcinkiewiczZygmundSymmConst (2 * m) * âˆ« Ï‰, (âˆ‘ i âˆˆ A, â€–X i Ï‰â€– ^ 2) ^ m âˆ‚Î¼ := by
  have : DecidableEq Î¹ := Classical.decEq _
  -- Turn the `L^p` assumption on the `X i` into various integrability conditions.
  have integrable_prod_norm_X I (hI : I âˆˆ A Ã—Ë¢ A ^^ m) :
    Integrable (fun Ï‰ â†¦ âˆ k, â€–X (I k).1 Ï‰â€– * â€–X (I k).2 Ï‰â€–) Î¼ := by
    sorry
  have integrable_prod_inner_X I (hI : I âˆˆ A Ã—Ë¢ A ^^ m) :
    Integrable (fun Ï‰ â†¦ âˆ k, inner â„ (X (I k).1 Ï‰) (X (I k).2 Ï‰)) Î¼ := sorry
  -- Call a family of indices `iâ‚, ..., iâ‚™, jâ‚, ..., jâ‚™` *even* if each `i âˆˆ A` appears an even
  -- number of times among the `2n` indices.
  let EvenIndex (I : Fin m â†’ Î¹ Ã— Î¹) : Prop :=
    âˆ€ i âˆˆ A, Even (#{k | (I k).1 = i} + #{k | (I k).2 = i})
  -- Now, let the calculation begin...
  calc
    âˆ« Ï‰, â€–âˆ‘ i âˆˆ A, X i Ï‰â€– ^ (2 * m) âˆ‚Î¼
    -- Expand out the power of the sum into a sum over families of indices
    -- `iâ‚, ..., iâ‚™, jâ‚, ..., jâ‚™` of `âˆ k, âŸ¨X iâ‚–, X jâ‚–âŸ©`. Push the integral inside the sum.
    _ = âˆ‘ I âˆˆ A Ã—Ë¢ A ^^ m, âˆ« Ï‰, âˆ k, inner â„ (X (I k).1 Ï‰) (X (I k).2 Ï‰) âˆ‚Î¼ := by
      simp_rw [pow_mul, â† real_inner_self_eq_norm_sq, sum_inner, inner_sum, â† sum_product',
        Finset.sum_pow', integral_finset_sum _ integrable_prod_inner_X]
    -- Show that the terms coming from odd families of indices `iâ‚, ..., iâ‚™, jâ‚, ..., jâ‚™` integrate
    -- to zero.
    _ = âˆ‘ I âˆˆ A Ã—Ë¢ A ^^ m with EvenIndex I, âˆ« Ï‰, âˆ k, inner â„ (X (I k).1 Ï‰) (X (I k).2 Ï‰) âˆ‚Î¼ := by
      rw [Finset.sum_filter_of_ne]
      -- Assume that `I = (iâ‚, ..., iâ‚™, jâ‚, ..., jâ‚™)` is an odd family.
      -- Say `i` appears an odd number of times in it.
      rintro I hI hI' i hi
      contrapose! hI'
      replace hI' : Odd (#{k | (I k).1 = i} + #{k | (I k).2 = i}) := by simpa using hI'
      -- Let `Y` be the family of random variables `X` where `X i` has been replaced by `-X i`.
      let Y : Î¹ â†’ Î© â†’ E := update X i (-X i)
      -- By the assumption that `X i` is symmetric, we get that `X j` and `Y j` are identically
      -- distributed for all `j`.
      have identDistrib_X_Y j : IdentDistrib (X j) (Y j) Î¼ Î¼ := by
        obtain rfl | hji := eq_or_ne j i
        Â· simpa [Y] using identDistrib_neg_X _
        Â· simpa [Y, hji] using .refl (identDistrib_neg_X _).aemeasurable_fst
      -- To show that `ğ”¼ âˆ k, âŸ¨X iâ‚–, X jâ‚–âŸ© = 0`, we will show
      -- `ğ”¼ âˆ k, âŸ¨X iâ‚–, X jâ‚–âŸ© = ğ”¼ âˆ k, âŸ¨Y iâ‚–, Y jâ‚–âŸ© = -ğ”¼ âˆ k, âŸ¨X iâ‚–, X jâ‚–âŸ©`.
      rw [â† neg_eq_self, â† integral_neg, eq_comm]
      calc
        -- `ğ”¼ âˆ k, âŸ¨X iâ‚–, X jâ‚–âŸ© = ğ”¼ âˆ k, âŸ¨Y iâ‚–, Y jâ‚–âŸ©` because `ğ”¼ âˆ k, âŸ¨X iâ‚–, X jâ‚–âŸ©` and
        -- `âˆ k, âŸ¨Y iâ‚–, Y jâ‚–âŸ©` are identically distributed.
        âˆ« Ï‰, âˆ k, inner â„ (X (I k).1 Ï‰) (X (I k).2 Ï‰) âˆ‚Î¼
        _ = âˆ« Ï‰, âˆ k, inner â„ (Y (I k).1 Ï‰) (Y (I k).2 Ï‰) âˆ‚Î¼ := by
          refine IdentDistrib.integral_eq ?_
          sorry -- TODO: Upstream result from PFR
        -- `ğ”¼ âˆ k, âŸ¨Y iâ‚–, Y jâ‚–âŸ© = -ğ”¼ âˆ k, âŸ¨X iâ‚–, X jâ‚–âŸ©` by the assumption that `i` appears an odd
        -- number of times in `I`.
        _ = âˆ« Ï‰, -âˆ k, inner â„ (X (I k).1 Ï‰) (X (I k).2 Ï‰) âˆ‚Î¼ := by
          congr with Ï‰
          calc
            âˆ k, inner â„ (Y (I k).1 Ï‰) (Y (I k).2 Ï‰)
            _ = âˆ k, (if (I k).1 = i then -1 else 1) * (if (I k).2 = i then -1 else 1) *
                inner â„ (X (I k).1 Ï‰) (X (I k).2 Ï‰) := by
              congr! with k; split_ifs with hkâ‚ hkâ‚‚ hkâ‚‚ <;> simp [hkâ‚, hkâ‚‚, Y]
            _ = -âˆ k, inner â„ (X (I k).1 Ï‰) (X (I k).2 Ï‰) := by
              rw [prod_mul_distrib, prod_mul_distrib]
              simp [prod_ite, â† pow_add, hI']
    -- Upper bound the sum by its absolute value and push the absolute value inside.
    _ â‰¤ |âˆ‘ I âˆˆ A Ã—Ë¢ A ^^ m with EvenIndex I, âˆ« Ï‰, âˆ k, inner â„ (X (I k).1 Ï‰) (X (I k).2 Ï‰) âˆ‚Î¼| :=
      le_abs_self _
    _ â‰¤ âˆ‘ I âˆˆ A Ã—Ë¢ A ^^ m with EvenIndex I, |âˆ« Ï‰, âˆ k, inner â„ (X (I k).1 Ï‰) (X (I k).2 Ï‰) âˆ‚Î¼| :=
      abs_sum_le_sum_abs ..
    _ â‰¤ âˆ‘ I âˆˆ A Ã—Ë¢ A ^^ m with EvenIndex I, âˆ« Ï‰, |âˆ k, inner â„ (X (I k).1 Ï‰) (X (I k).2 Ï‰)| âˆ‚Î¼ := by
      gcongr with I; exact abs_integral_le_integral_abs
    _ = âˆ‘ I âˆˆ A Ã—Ë¢ A ^^ m with EvenIndex I, âˆ« Ï‰, âˆ k, |inner â„ (X (I k).1 Ï‰) (X (I k).2 Ï‰)| âˆ‚Î¼ := by
      simp_rw [abs_prod]
    -- Finish pushing the absolute value inside using Cauchy-Schwarz.
    _ â‰¤ âˆ‘ I âˆˆ A Ã—Ë¢ A ^^ m with EvenIndex I, âˆ« Ï‰, âˆ k, â€–X (I k).1 Ï‰â€– * â€–X (I k).2 Ï‰â€– âˆ‚Î¼ := by
      gcongr with I hI
      Â· simpa [abs_prod] using (integrable_prod_inner_X I <| filter_subset _ _ hI).abs
      Â· exact integrable_prod_norm_X I <| filter_subset _ _ hI
      rintro Ï‰
      dsimp
      gcongr with k
      exact abs_real_inner_le_norm ..
    -- Rewrite the sum of `ğ”¼ âˆ k, â€–X iâ‚– Ï‰â€– * â€–X jâ‚– Ï‰â€–` over even families of indices
    -- `iâ‚, ..., iâ‚™, jâ‚, ..., jâ‚™` into the sum over `wâ‚ + ... + wâ‚ = m` of
    -- `(2m choose 2wâ‚, ..., 2wâ‚) * ğ”¼ âˆ i, â€–X iâ€– ^ wáµ¢`.
    _ = âˆ‘ I âˆˆ A Ã—Ë¢ A ^^ m with EvenIndex I,
          âˆ« Ï‰, âˆ i âˆˆ A, â€–X i Ï‰â€– ^ (#{k | (I k).1 = i} + #{k | (I k).2 = i}) âˆ‚Î¼ := by
      congr! with I hI Ï‰
      simp only [mem_filter, mem_piFinset, mem_product, forall_and] at hI
      simp_rw [pow_add, prod_mul_distrib, â† prod_const]
      rw [prod_fiberwise_of_maps_to', prod_fiberwise_of_maps_to']
      Â· simpa using hI.1.2
      Â· simpa using hI.1.1
    _ = âˆ‘ w âˆˆ piAntidiag A (2 * m) with âˆ€ i âˆˆ A, 2 âˆ£ w i,
          multinomial A w * âˆ« Ï‰, âˆ i âˆˆ A, â€–X i Ï‰â€– ^ w i âˆ‚Î¼ := by
      sorry
    _ = âˆ‘ w âˆˆ (piAntidiag A m).map
          âŸ¨(2 â€¢ Â·), fun _ _ h â†¦ funext fun i â†¦ mul_right_injectiveâ‚€ two_ne_zero (congr_fun h i)âŸ©,
        multinomial A w * âˆ« Ï‰, âˆ i âˆˆ A, â€–X i Ï‰â€– ^ w i âˆ‚Î¼ := by
      rw [map_nsmul_piAntidiag _ _ two_ne_zero]
    _ = âˆ‘ w âˆˆ piAntidiag A m, multinomial A (2 â€¢ w) * âˆ« Ï‰, âˆ i âˆˆ A, â€–X i Ï‰â€– ^ (2 * w i) âˆ‚Î¼ := by
      simp
    -- Use the fact that `(2m choose 2wâ‚, ..., 2wâ‚) â‰¤ m ^ m * (m choose wâ‚, ..., wâ‚)`.
    _ â‰¤ âˆ‘ w âˆˆ piAntidiag A m, marcinkiewiczZygmundSymmConst (2 * m) * multinomial A w *
          âˆ« Ï‰, âˆ i âˆˆ A, â€–X i Ï‰â€– ^ (2 * w i) âˆ‚Î¼ := by
      gcongr with w hw
      calc
        (multinomial A (2 â€¢ w) : â„)
        _ â‰¤ ((âˆ‘ i âˆˆ A, w i) ^ âˆ‘ i âˆˆ A, w i) * multinomial A w :=
          mod_cast multinomial_two_mul_le_mul_multinomial
        _ = marcinkiewiczZygmundSymmConst (2 * m) * multinomial A w := by
          simp [(mem_piAntidiag.1 hw).1, marcinkiewiczZygmundSymmConst]
    -- Put the sum back together.
    _ = marcinkiewiczZygmundSymmConst (2 * m) * âˆ« Ï‰, (âˆ‘ i âˆˆ A, â€–X i Ï‰â€– ^ 2) ^ m âˆ‚Î¼ := by
      simp_rw [sum_pow_eq_sum_piAntidiag, â† pow_mul, â† integral_const_mul, mul_sum, â† mul_assoc]
      rw [integral_finset_sum]
      rintro w hw
      exact .const_mul sorry _

/-- The constant appearing in the Marcinkiewicz-Zygmund inequality for random variables with zero
mean. -/
noncomputable def marcinkiewiczZygmundConst (p : â„â‰¥0) : â„ :=
  2 ^ (p / 2 : â„) * marcinkiewiczZygmundSymmConst p

/-- The **Marcinkiewicz-Zygmund inequality** for random variables with zero mean.

For symmetric random variables, `marcinkiewicz_zygmund` provides a slightly better constant. -/
theorem marcinkiewicz_zygmund (iIndepFun_X : iIndepFun X Î¼)
    (integral_X : âˆ€ i, âˆ« Ï‰, X i Ï‰ âˆ‚Î¼ = 0)
    (memLp_X : âˆ€ i âˆˆ A, MemLp (X i) (2 * m) Î¼) :
    âˆ« Ï‰, â€–âˆ‘ i âˆˆ A, X i Ï‰â€– ^ (2 * m) âˆ‚Î¼ â‰¤
      marcinkiewiczZygmundConst (2 * m) * âˆ« Ï‰, (âˆ‘ i âˆˆ A, â€–X i Ï‰â€– ^ 2) ^ m âˆ‚Î¼ := by
  let Xâ‚ i : Î© Ã— Î© â†’ E := X i âˆ˜ Prod.fst
  let Xâ‚‚ i : Î© Ã— Î© â†’ E := X i âˆ˜ Prod.snd
  let X' i : Î© Ã— Î© â†’ E := Xâ‚ i - Xâ‚‚ i
  have : DecidableEq Î¹ := Classical.decEq _
  calc
    âˆ« Ï‰, â€–âˆ‘ i âˆˆ A, X i Ï‰â€– ^ (2 * m) âˆ‚Î¼
    _ â‰¤ âˆ« Ï‰, â€–âˆ‘ i âˆˆ A, X' i Ï‰â€– ^ (2 * m) âˆ‚Î¼.prod Î¼ := by
      sorry
    _ â‰¤ marcinkiewiczZygmundSymmConst (2 * m) * âˆ« Ï‰, (âˆ‘ i âˆˆ A, â€–X' i Ï‰â€– ^ 2) ^ m âˆ‚Î¼.prod Î¼ :=
      marcinkiewicz_zygmund_symmetric sorry (fun i â†¦ sorry) sorry
    _ â‰¤ marcinkiewiczZygmundConst (2 * m) * âˆ« Ï‰, (âˆ‘ i âˆˆ A, â€–X i Ï‰â€– ^ 2) ^ m âˆ‚Î¼ := sorry
