/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Lorenzo Luccioli
-/
import Mathlib.Probability.Decision.RiskIncrease

/-!
# Statistical information

## Main definitions

* `deGrootInfo`

## Main statements

* `deGrootInfo_comp_le`: data-processing inequality

## Notation

## Implementation details

-/

open MeasureTheory Set

open scoped ENNReal NNReal

namespace ProbabilityTheory

variable {ğ“§ ğ“¨ : Type*} {mğ“§ : MeasurableSpace ğ“§} {mğ“¨ : MeasurableSpace ğ“¨}
  {Î¼ Î½ : Measure ğ“§} {Ï€ : Measure Bool}

/-- The statistical information of the measures `Î¼` and `Î½` with respect to
the prior `Ï€` on `Bool`.
This is the difference of the Bayes risks between estimation without seeing the data and with it. -/
noncomputable
def deGrootInfo (Î¼ Î½ : Measure ğ“§) (Ï€ : Measure Bool) : â„â‰¥0âˆ :=
  bayesBinaryRisk (Kernel.discard ğ“§ âˆ˜â‚˜ Î¼) (Kernel.discard ğ“§ âˆ˜â‚˜ Î½) Ï€ - bayesBinaryRisk Î¼ Î½ Ï€

lemma deGrootInfo_eq_riskIncrease :
  deGrootInfo Î¼ Î½ Ï€ = riskIncrease binaryLoss (boolKernel Î¼ Î½) Ï€ := by
  simp only [deGrootInfo, Measure.discard_comp, riskIncrease, Kernel.comp_discard',
    boolKernel_apply, bayesBinaryRisk]
  congr with a
  cases a <;> simp

lemma deGrootInfo_eq_min_sub (Î¼ Î½ : Measure ğ“§) (Ï€ : Measure Bool) :
    deGrootInfo Î¼ Î½ Ï€ = min (Ï€ {false} * Î¼ univ) (Ï€ {true} * Î½ univ) - bayesBinaryRisk Î¼ Î½ Ï€ := by
  simp_rw [deGrootInfo, Measure.discard_comp, bayesBinaryRisk_dirac]

@[simp] lemma deGrootInfo_zero_left : deGrootInfo 0 Î½ Ï€ = 0 := by simp [deGrootInfo]

@[simp] lemma deGrootInfo_zero_right : deGrootInfo Î¼ 0 Ï€ = 0 := by simp [deGrootInfo]

@[simp] lemma deGrootInfo_zero_prior : deGrootInfo Î¼ Î½ 0 = 0 := by simp [deGrootInfo]

@[simp] lemma deGrootInfo_self : deGrootInfo Î¼ Î¼ Ï€ = 0 := by simp [deGrootInfo]

lemma deGrootInfo_le_min : deGrootInfo Î¼ Î½ Ï€ â‰¤ min (Ï€ {false} * Î¼ univ) (Ï€ {true} * Î½ univ) :=
  deGrootInfo_eq_min_sub Î¼ Î½ Ï€ â–¸ tsub_le_self

lemma deGrootInfo_ne_top [IsFiniteMeasure Î¼] [IsFiniteMeasure Ï€] : deGrootInfo Î¼ Î½ Ï€ â‰  âŠ¤ :=
  (deGrootInfo_le_min.trans_lt <| min_lt_iff.mpr <| Or.inl
    <| ENNReal.mul_lt_top (measure_lt_top Ï€ _) (measure_lt_top Î¼ _)).ne

lemma deGrootInfo_comm : deGrootInfo Î¼ Î½ Ï€ = deGrootInfo Î½ Î¼ (Ï€.map Bool.not) := by
  simp_rw [deGrootInfo, bayesBinaryRisk_comm _ _ Ï€]

lemma deGrootInfo_of_measure_true_eq_zero (Î¼ Î½ : Measure ğ“§) (hÏ€ : Ï€ {true} = 0) :
    deGrootInfo Î¼ Î½ Ï€ = 0 :=
  le_antisymm (deGrootInfo_le_min.trans (by simp [hÏ€])) zero_le'

lemma deGrootInfo_of_measure_false_eq_zero (Î¼ Î½ : Measure ğ“§) (hÏ€ : Ï€ {false} = 0) :
    deGrootInfo Î¼ Î½ Ï€ = 0 :=
  le_antisymm (deGrootInfo_le_min.trans (by simp [hÏ€])) zero_le'

/-- **Data processing inequality** for the statistical information. -/
lemma deGrootInfo_comp_le (Î¼ Î½ : Measure ğ“§) (Ï€ : Measure Bool) (Î· : Kernel ğ“§ ğ“¨) [IsMarkovKernel Î·] :
    deGrootInfo (Î· âˆ˜â‚˜ Î¼) (Î· âˆ˜â‚˜ Î½) Ï€ â‰¤ deGrootInfo Î¼ Î½ Ï€ := by
  simp_rw [deGrootInfo_eq_riskIncrease, â† comp_boolKernel]
  exact riskIncrease_comp_le binaryLoss (boolKernel Î¼ Î½) Ï€ Î·

lemma deGrootInfo_eq_deGrootInfo_one_one :
    deGrootInfo Î¼ Î½ Ï€ = deGrootInfo (Ï€ {false} â€¢ Î¼) (Ï€ {true} â€¢ Î½) (Bool.boolMeasure 1 1) := by
  rw [deGrootInfo, bayesBinaryRisk_eq_bayesBinaryRisk_one_one]
  nth_rw 2 [bayesBinaryRisk_eq_bayesBinaryRisk_one_one]
  simp [deGrootInfo]

lemma deGrootInfo_boolMeasure_le_deGrootInfo {E : Set ğ“§} (hE : MeasurableSet E) :
    deGrootInfo (Bool.boolMeasure (Î¼ Eá¶œ) (Î¼ E)) (Bool.boolMeasure (Î½ Eá¶œ) (Î½ E)) Ï€
      â‰¤ deGrootInfo Î¼ Î½ Ï€ := by
  have h_meas : Measurable fun x â†¦ Bool.ofNat (E.indicator 1 x) :=
    (Measurable.of_discrete.comp' (measurable_one.indicator hE))
  let Î· : Kernel ğ“§ Bool := Kernel.deterministic (fun x â†¦ Bool.ofNat (E.indicator 1 x)) h_meas
  have h_false : (fun x â†¦ Bool.ofNat (E.indicator 1 x)) â»Â¹' {false} = Eá¶œ := by
    ext x; simp [Bool.ofNat]
  have h_true : (fun x â†¦ Bool.ofNat (E.indicator 1 x)) â»Â¹' {true} = E := by
    ext x; simp [Bool.ofNat]
  convert deGrootInfo_comp_le Î¼ Î½ Ï€ Î· <;>
  Â· ext
    Â· rw [Measure.deterministic_comp_eq_map, Measure.map_apply h_meas (by trivial), h_false,
        Bool.boolMeasure_apply_false]
    Â· rw [Measure.deterministic_comp_eq_map, Measure.map_apply h_meas (by trivial), h_true,
        Bool.boolMeasure_apply_true]

lemma deGrootInfo_eq_min_sub_lintegral (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    deGrootInfo Î¼ Î½ Ï€ = min (Ï€ {false} * Î¼ univ) (Ï€ {true} * Î½ univ)
      - âˆ«â» x, min (Ï€ {false} * Î¼.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x)
      (Ï€ {true} * Î½.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x) âˆ‚(boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) := by
  rw [deGrootInfo_eq_min_sub, bayesBinaryRisk_eq_lintegral_min]

lemma deGrootInfo_eq_min_sub_lintegral' {Î¶ : Measure ğ“§} [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    [SigmaFinite Î¶] (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] (hÎ¼Î¶ : Î¼ â‰ª Î¶) (hÎ½Î¶ : Î½ â‰ª Î¶) :
    deGrootInfo Î¼ Î½ Ï€ = min (Ï€ {false} * Î¼ univ) (Ï€ {true} * Î½ univ)
      - âˆ«â» x, min (Ï€ {false} * (âˆ‚Î¼/âˆ‚Î¶) x) (Ï€ {true} * (âˆ‚Î½/âˆ‚Î¶) x) âˆ‚Î¶ := by
  by_cases h_false : Ï€ {false} = 0
  Â· simp [deGrootInfo, h_false, bayesBinaryRisk_of_measure_false_eq_zero]
  by_cases h_true : Ï€ {true} = 0
  Â· simp [deGrootInfo, h_true, bayesBinaryRisk_of_measure_true_eq_zero]
  have hÎ¼ac : Î¼ â‰ª (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) :=
    absolutelyContinuous_boolKernel_comp_measure_left Î¼ Î½ h_false
  have hÎ½ac : Î½ â‰ª (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) :=
    absolutelyContinuous_boolKernel_comp_measure_right Î¼ Î½ h_true
  have hacÎ¶ : (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) â‰ª Î¶ :=
    boolKernel_comp_measure Î¼ Î½ Ï€ â–¸ (hÎ½Î¶.smul_left _).add_left (hÎ¼Î¶.smul_left _)
  rw [deGrootInfo_eq_min_sub_lintegral, â† lintegral_rnDeriv_mul hacÎ¶ (by fun_prop)]
  congr 1
  apply lintegral_congr_ae
  filter_upwards [Measure.rnDeriv_mul_rnDeriv hÎ¼ac, Measure.rnDeriv_mul_rnDeriv hÎ½ac] with x hxÎ¼ hxÎ½
  rw [ENNReal.mul_min, mul_comm, mul_comm _ (Ï€ _ * _), mul_assoc, mul_assoc]
  congr

lemma deGrootInfo_eq_min_sub_iInf_measurableSet (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼]
    [IsFiniteMeasure Î½] (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    deGrootInfo Î¼ Î½ Ï€ = min (Ï€ {false} * Î¼ univ) (Ï€ {true} * Î½ univ)
      - â¨… E, â¨… (_ : MeasurableSet E), Ï€ {false} * Î¼ E + Ï€ {true} * Î½ Eá¶œ := by
  rw [deGrootInfo_eq_min_sub, bayesBinaryRisk_eq_iInf_measurableSet]

lemma deGrootInfo_eq_iSup_measurableSet_of_measure_univ_le (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼]
    [IsFiniteMeasure Î½] (Ï€ : Measure Bool) [IsFiniteMeasure Ï€]
    (h : Ï€ {true} * Î½ univ â‰¤ Ï€ {false} * Î¼ univ) :
    deGrootInfo Î¼ Î½ Ï€ = â¨† E, â¨† (_ : MeasurableSet E), Ï€ {true} * Î½ E - Ï€ {false} * Î¼ E := by
  rw [deGrootInfo_eq_min_sub_iInf_measurableSet, min_eq_right h]
  calc Ï€ {true} * Î½ univ - â¨… (E) (_ : MeasurableSet E), Ï€ {false} * Î¼ E + Ï€ {true} * Î½ Eá¶œ
  _ = Ï€ {true} * Î½ univ - â¨… (E) (_ : MeasurableSet E), Ï€ {false} * Î¼ E +
      (Ï€ {true} * Î½ univ - Ï€ {true} * Î½ E) := by
    congr with E
    congr with hE
    congr
    rw [measure_compl hE (measure_ne_top _ _), ENNReal.mul_sub (by simp)]
  _ = â¨† (E) (_ : MeasurableSet E), Ï€ {true} * Î½ E - Ï€ {false} * Î¼ E := by
    simp_rw [ENNReal.sub_iInf]
    congr with E
    congr with hE
    rcases le_total (Ï€ {true} * Î½ E) (Ï€ {false} * Î¼ E) with hE_le | hE_le
    Â· rw [tsub_eq_zero_of_le hE_le]
      refine tsub_eq_zero_of_le ?_
      calc Ï€ {true} * Î½ univ
      _ = Ï€ {true} * Î½ E + (Ï€ {true} * Î½ univ - Ï€ {true} * Î½ E) := by
        rw [add_comm, ENNReal.sub_add_eq_add_sub, ENNReal.add_sub_cancel_right]
        Â· finiteness
        Â· have : E âŠ† univ := subset_univ E
          gcongr
        Â· finiteness
      _ â‰¤ Ï€ {false} * Î¼ E + (Ï€ {true} * Î½ univ - Ï€ {true} * Î½ E) := by gcongr
    Â· rw [add_comm]
      calc Ï€ {true} * Î½ univ - (Ï€ {true} * Î½ univ - Ï€ {true} * Î½ E + Ï€ {false} * Î¼ E)
      _ = (Ï€ {true} * Î½ univ - Ï€ {true} * Î½ E + Ï€ {true} * Î½ E)
          - (Ï€ {true} * Î½ univ - Ï€ {true} * Î½ E + Ï€ {false} * Î¼ E) := by
        congr
        rw [ENNReal.sub_add_eq_add_sub, ENNReal.add_sub_cancel_right]
        Â· finiteness
        Â· have : E âŠ† univ := subset_univ E
          gcongr
        Â· finiteness
      _ = Ï€ {true} * Î½ E - Ï€ {false} * Î¼ E := by
        rw [ENNReal.add_sub_add_eq_sub_left (by finiteness)]

lemma deGrootInfo_eq_iSup_measurableSet_of_measure_univ_le' (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼]
    [IsFiniteMeasure Î½] (Ï€ : Measure Bool) [IsFiniteMeasure Ï€]
    (h : Ï€ {false} * Î¼ univ â‰¤ Ï€ {true} * Î½ univ) :
    deGrootInfo Î¼ Î½ Ï€ = â¨† E, â¨† (_ : MeasurableSet E), Ï€ {false} * Î¼ E - Ï€ {true} * Î½ E := by
  rw [deGrootInfo_comm, deGrootInfo_eq_iSup_measurableSet_of_measure_univ_le]
  Â· simp
  Â· simpa using h

lemma deGrootInfo_eq_zero_of_le [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½] [IsFiniteMeasure Ï€]
    (hÎ½Î¼ : Ï€ {true} â€¢ Î½ â‰¤ Ï€ {false} â€¢ Î¼) :
    deGrootInfo Î¼ Î½ Ï€ = 0 := by
  have h_le s : Ï€ {true} * Î½ s â‰¤ Ï€ {false} * Î¼ s := hÎ½Î¼ s
  rw [deGrootInfo_eq_iSup_measurableSet_of_measure_univ_le _ _ _ (h_le .univ)]
  simp [tsub_eq_zero_iff_le, h_le]

lemma deGrootInfo_eq_zero_iff [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½] [IsFiniteMeasure Ï€]
    (h_univ : Ï€ {false} * Î¼ univ = Ï€ {true} * Î½ univ) :
    deGrootInfo Î¼ Î½ Ï€ = 0 â†” Ï€ {false} â€¢ Î¼ = Ï€ {true} â€¢ Î½ := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· have : IsFiniteMeasure (Ï€ {false} â€¢ Î¼) := isFiniteMeasure_smul (by finiteness) _
    have : IsFiniteMeasure (Ï€ {true} â€¢ Î½) := isFiniteMeasure_smul (by finiteness) _
    refine Measure.eq_of_le_of_measure_univ_eq ?_ (by simp [h_univ])
    refine Measure.le_intro fun s hs _ â†¦ ?_
    rw [deGrootInfo_eq_iSup_measurableSet_of_measure_univ_le' _ _ _ h_univ.le] at h
    simp only [ENNReal.iSup_eq_zero, tsub_eq_zero_iff_le] at h
    exact h s hs
  Â· rw [deGrootInfo_eq_deGrootInfo_one_one, h, deGrootInfo_self]

end ProbabilityTheory
