/-
Copyright (c) 2024 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Lorenzo Luccioli
-/
import Mathlib.Probability.Decision.BoolMeasure
import Mathlib.Probability.Decision.Risk

/-!
# Kernel with two values

-/

open MeasureTheory

open scoped ENNReal NNReal

namespace ProbabilityTheory

variable {ğ“§ ğ“¨ : Type*} {mğ“§ : MeasurableSpace ğ“§} {mğ“¨ : MeasurableSpace ğ“¨} {Î¼ Î½ : Measure ğ“§}

/-- The kernel that sends `false` to `Î¼` and `true` to `Î½`. -/
def boolKernel (Î¼ Î½ : Measure ğ“§) : Kernel Bool ğ“§ where
  toFun := fun b â†¦ if b then Î½ else Î¼
  measurable' := .of_discrete

@[simp] lemma boolKernel_true : boolKernel Î¼ Î½ true = Î½ := rfl

@[simp] lemma boolKernel_false : boolKernel Î¼ Î½ false = Î¼ := rfl

@[simp] lemma boolKernel_apply (b : Bool) : boolKernel Î¼ Î½ b = if b then Î½ else Î¼ := rfl

instance [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½] : IsFiniteKernel (boolKernel Î¼ Î½) :=
  âŸ¨max (Î¼ .univ) (Î½ .univ), max_lt (measure_lt_top _ _) (measure_lt_top _ _),
    fun b â†¦ by cases b <;> simpâŸ©

instance [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î½] :
    IsMarkovKernel (boolKernel Î¼ Î½) := by
  refine âŸ¨fun b â†¦ ?_âŸ©
  simp only [boolKernel_apply]
  split <;> infer_instance

lemma Kernel_bool_eq_boolKernel (Îº : Kernel Bool ğ“§) :
    Îº = boolKernel (Îº false) (Îº true) := by
  ext (_ | _) <;> simp

@[simp]
lemma comp_boolKernel (Îº : Kernel ğ“§ ğ“¨) :
    Îº âˆ˜â‚– (boolKernel Î¼ Î½) = boolKernel (Îº âˆ˜â‚˜ Î¼) (Îº âˆ˜â‚˜ Î½) := by
  ext b : 1
  rw [Kernel.comp_apply]
  cases b with
  | false => simp
  | true => simp

lemma boolKernel_comp_measure (Î¼ Î½ : Measure ğ“§) (Ï€ : Measure Bool) :
    boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€ = Ï€ {true} â€¢ Î½ + Ï€ {false} â€¢ Î¼ := by
  ext s hs
  rw [Measure.bind_apply hs (Kernel.aemeasurable _)]
  simp only [boolKernel_apply, lintegral_fintype, Fintype.univ_bool, Finset.mem_singleton,
    Bool.true_eq_false, not_false_eq_true, Finset.sum_insert, â†“reduceIte, Finset.sum_singleton,
    Bool.false_eq_true, Measure.coe_add, Measure.coe_smul, Pi.add_apply, Pi.smul_apply, smul_eq_mul]
  congr 1 <;> rw [mul_comm]

lemma absolutelyContinuous_boolKernel_comp_measure_left (Î¼ Î½ : Measure ğ“§)
    {Ï€ : Measure Bool} (hÏ€ : Ï€ {false} â‰  0) :
    Î¼ â‰ª boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€ :=
  boolKernel_comp_measure _ _ _ â–¸ add_comm _ (Ï€ {true} â€¢ Î½) â–¸
    (Measure.absolutelyContinuous_smul hÏ€).add_right _

lemma absolutelyContinuous_boolKernel_comp_measure_right (Î¼ Î½ : Measure ğ“§)
    {Ï€ : Measure Bool} (hÏ€ : Ï€ {true} â‰  0) :
    Î½ â‰ª boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€ :=
  boolKernel_comp_measure _ _ _ â–¸ (Measure.absolutelyContinuous_smul hÏ€).add_right _

lemma sum_smul_rnDeriv_boolKernel (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    (Ï€ {true} â€¢ Î½.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) + Ï€ {false} â€¢ (Î¼.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€)))
      =áµ[boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€] 1 := by
  have h1 := Î½.rnDeriv_smul_left_of_ne_top (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€)
    (measure_ne_top Ï€ {true})
  have h2 := Î¼.rnDeriv_smul_left_of_ne_top (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€)
    (measure_ne_top Ï€ {false})
  have : IsFiniteMeasure (Ï€ {true} â€¢ Î½) := Î½.smul_finite (measure_ne_top _ _)
  have : IsFiniteMeasure (Ï€ {false} â€¢ Î¼) := Î¼.smul_finite (measure_ne_top _ _)
  have h3 := (Ï€ {true} â€¢ Î½).rnDeriv_add  (Ï€ {false} â€¢ Î¼) (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€)
  have h4 := (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€).rnDeriv_self
  filter_upwards [h1, h2, h3, h4] with a h1 h2 h3 h4
  simp only [Pi.add_apply, Pi.smul_apply, smul_eq_mul, Pi.one_apply] at h1 h2 h3 h4 âŠ¢
  rw [â† h1, â† h2, â† h3, â† boolKernel_comp_measure, h4]

lemma sum_smul_rnDeriv_boolKernel' (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    âˆ€áµ x âˆ‚(boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€), Ï€ {true} * Î½.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x
      + Ï€ {false} * Î¼.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x = 1 := by
  filter_upwards [sum_smul_rnDeriv_boolKernel Î¼ Î½ Ï€] with x hx using by simpa using hx

lemma posterior_boolKernel_apply_false (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    âˆ€áµ x âˆ‚boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€, ((boolKernel Î¼ Î½)â€ Ï€) x {false}
      = Ï€ {false} * Î¼.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x := by
  filter_upwards [posterior_eq_withDensity_of_countable (boolKernel Î¼ Î½) Ï€] with x hx
  rw [hx]
  simp

lemma posterior_boolKernel_apply_true (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    âˆ€áµ x âˆ‚boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€, ((boolKernel Î¼ Î½)â€ Ï€) x {true}
      = Ï€ {true} * Î½.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x := by
  filter_upwards [posterior_eq_withDensity_of_countable (boolKernel Î¼ Î½) Ï€] with x hx
  rw [hx]
  simp

end ProbabilityTheory
