/-
Copyright (c) 2024 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Lorenzo Luccioli
-/
import Mathlib.Probability.Decision.BoolMeasure
import Mathlib.Probability.Decision.Risk

/-!
# Kernel with two values

-/

open MeasureTheory

open scoped ENNReal NNReal

namespace ProbabilityTheory

variable {Î˜ ğ“§ ğ“§' ğ“¨ ğ“© : Type*} {mÎ˜ : MeasurableSpace Î˜} {mğ“§ : MeasurableSpace ğ“§}
  {mğ“§' : MeasurableSpace ğ“§'} {mğ“¨ : MeasurableSpace ğ“¨} {mğ“© : MeasurableSpace ğ“©}
  {Î¼ Î½ : Measure ğ“§} {p : â„â‰¥0âˆ}

/-- The kernel that sends `false` to `Î¼` and `true` to `Î½`. -/
def boolKernel (Î¼ Î½ : Measure ğ“§) : Kernel Bool ğ“§ where
  toFun := fun b â†¦ if b then Î½ else Î¼
  measurable' := .of_discrete

@[simp] lemma boolKernel_true : boolKernel Î¼ Î½ true = Î½ := rfl

@[simp] lemma boolKernel_false : boolKernel Î¼ Î½ false = Î¼ := rfl

@[simp] lemma boolKernel_apply (b : Bool) : boolKernel Î¼ Î½ b = if b then Î½ else Î¼ := rfl

instance [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½] : IsFiniteKernel (boolKernel Î¼ Î½) :=
  âŸ¨max (Î¼ .univ) (Î½ .univ), max_lt (measure_lt_top _ _) (measure_lt_top _ _),
    fun b â†¦ by cases b <;> simpâŸ©

instance [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î½] :
    IsMarkovKernel (boolKernel Î¼ Î½) := by
  refine âŸ¨fun b â†¦ ?_âŸ©
  simp only [boolKernel_apply]
  split <;> infer_instance

lemma Kernel_bool_eq_boolKernel (Îº : Kernel Bool ğ“§) :
    Îº = boolKernel (Îº false) (Îº true) := by
  ext (_ | _) <;> simp

@[simp]
lemma comp_boolKernel (Îº : Kernel ğ“§ ğ“¨) :
    Îº âˆ˜â‚– (boolKernel Î¼ Î½) = boolKernel (Îº âˆ˜â‚˜ Î¼) (Îº âˆ˜â‚˜ Î½) := by
  ext b : 1
  rw [Kernel.comp_apply]
  cases b with
  | false => simp
  | true => simp

lemma measure_comp_boolKernel (Î¼ Î½ : Measure ğ“§) (Ï€ : Measure Bool) :
    boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€ = Ï€ {true} â€¢ Î½ + Ï€ {false} â€¢ Î¼ := by
  ext s hs
  rw [Measure.bind_apply hs (Kernel.aemeasurable _)]
  simp only [boolKernel_apply, lintegral_fintype, Fintype.univ_bool, Finset.mem_singleton,
    Bool.true_eq_false, not_false_eq_true, Finset.sum_insert, â†“reduceIte, Finset.sum_singleton,
    Bool.false_eq_true, Measure.coe_add, Measure.coe_smul, Pi.add_apply, Pi.smul_apply, smul_eq_mul]
  congr 1 <;> rw [mul_comm]

lemma absolutelyContinuous_measure_comp_boolKernel_left (Î¼ Î½ : Measure ğ“§)
    {Ï€ : Measure Bool} (hÏ€ : Ï€ {false} â‰  0) :
    Î¼ â‰ª boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€ :=
  measure_comp_boolKernel _ _ _ â–¸ add_comm _ (Ï€ {true} â€¢ Î½) â–¸
    (Measure.absolutelyContinuous_smul hÏ€).add_right _

lemma absolutelyContinuous_measure_comp_boolKernel_right (Î¼ Î½ : Measure ğ“§)
    {Ï€ : Measure Bool} (hÏ€ : Ï€ {true} â‰  0) :
    Î½ â‰ª boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€ :=
  measure_comp_boolKernel _ _ _ â–¸ (Measure.absolutelyContinuous_smul hÏ€).add_right _

lemma sum_smul_rnDeriv_boolKernel (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    (Ï€ {true} â€¢ Î½.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) + Ï€ {false} â€¢ (Î¼.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€)))
      =áµ[boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€] 1 := by
  have h1 := Î½.rnDeriv_smul_left_of_ne_top (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€)
    (measure_ne_top Ï€ {true})
  have h2 := Î¼.rnDeriv_smul_left_of_ne_top (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€)
    (measure_ne_top Ï€ {false})
  have : IsFiniteMeasure (Ï€ {true} â€¢ Î½) := Î½.smul_finite (measure_ne_top _ _)
  have : IsFiniteMeasure (Ï€ {false} â€¢ Î¼) := Î¼.smul_finite (measure_ne_top _ _)
  have h3 := (Ï€ {true} â€¢ Î½).rnDeriv_add  (Ï€ {false} â€¢ Î¼) (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€)
  have h4 := (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€).rnDeriv_self
  filter_upwards [h1, h2, h3, h4] with a h1 h2 h3 h4
  simp only [Pi.add_apply, Pi.smul_apply, smul_eq_mul, Pi.one_apply] at h1 h2 h3 h4 âŠ¢
  rw [â† h1, â† h2, â† h3, â† measure_comp_boolKernel, h4]

lemma sum_smul_rnDeriv_boolKernel' (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    âˆ€áµ x âˆ‚(boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€), Ï€ {true} * Î½.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x
      + Ï€ {false} * Î¼.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x = 1 := by
  filter_upwards [sum_smul_rnDeriv_boolKernel Î¼ Î½ Ï€] with x hx
  simpa using hx

/-- The kernel from `ğ“§` to `Bool` defined by
`x â†¦ (Ï€â‚€ * âˆ‚Î¼/âˆ‚(boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x + Ï€â‚ * âˆ‚Î½/âˆ‚(boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x)`.
It is the Bayesian inverse of `boolKernel Î¼ Î½` with respect to `Ï€`
(see `bayesInv_boolKernel`). -/
noncomputable
def boolKernelInv (Î¼ Î½ : Measure ğ“§) (Ï€ : Measure Bool) : Kernel ğ“§ Bool where
  toFun x :=
    if Ï€ {true} * Î½.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x
      + Ï€ {false} * Î¼.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x = 1
    then (Ï€ {true} * Î½.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x) â€¢ Measure.dirac true
      + (Ï€ {false} * Î¼.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x) â€¢ Measure.dirac false
    else Measure.dirac true
  measurable' := by
    refine Measurable.ite ?_ ?_ measurable_const
    Â· refine measurableSet_preimage ?_ (measurableSet_singleton _)
      exact ((Î½.measurable_rnDeriv _).const_mul _).add ((Î¼.measurable_rnDeriv _).const_mul _)
    refine Measure.measurable_of_measurable_coe _ (fun s _ â†¦ ?_)
    simp only [Measure.coe_add, Measure.coe_smul, Pi.add_apply, Pi.smul_apply,
      MeasurableSpace.measurableSet_top, Measure.dirac_apply', smul_eq_mul]
    exact ((measurable_const.mul (Î½.measurable_rnDeriv _)).mul measurable_const).add
      ((measurable_const.mul (Î¼.measurable_rnDeriv _)).mul measurable_const)

lemma boolKernelInv_apply (Î¼ Î½ : Measure ğ“§) (Ï€ : Measure Bool) (x : ğ“§) :
    boolKernelInv Î¼ Î½ Ï€ x
      = if Ï€ {true} * Î½.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x
          + Ï€ {false} * Î¼.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x = 1
        then (Ï€ {true} * Î½.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x) â€¢ Measure.dirac true
          + (Ï€ {false} * Î¼.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x) â€¢ Measure.dirac false
        else Measure.dirac true := rfl

lemma boolKernelInv_apply_ae (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    âˆ€áµ x âˆ‚(boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€), boolKernelInv Î¼ Î½ Ï€ x
      = (Ï€ {true} * Î½.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x) â€¢ Measure.dirac true
        + (Ï€ {false} * Î¼.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x) â€¢ Measure.dirac false := by
  filter_upwards [sum_smul_rnDeriv_boolKernel' Î¼ Î½ Ï€] with x hx
  rw [boolKernelInv_apply, if_pos hx]

lemma boolKernelInv_apply' (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] (s : Set Bool) :
    âˆ€áµ x âˆ‚(boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€), boolKernelInv Î¼ Î½ Ï€ x s
      = Ï€ {true} * Î½.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x * s.indicator 1 true
        + Ï€ {false} * Î¼.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x * s.indicator 1 false := by
  filter_upwards [boolKernelInv_apply_ae Î¼ Î½ Ï€] with x hx
  rw [hx]
  simp

lemma boolKernelInv_apply_false (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    âˆ€áµ x âˆ‚(boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€),
      boolKernelInv Î¼ Î½ Ï€ x {false} = Ï€ {false} * Î¼.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x := by
  filter_upwards [boolKernelInv_apply_ae Î¼ Î½ Ï€] with x hx
  simp [hx]

lemma boolKernelInv_apply_true (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    âˆ€áµ x âˆ‚(boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€),
      boolKernelInv Î¼ Î½ Ï€ x {true} = Ï€ {true} * Î½.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x := by
  filter_upwards [boolKernelInv_apply_ae Î¼ Î½ Ï€] with x hx
  simp [hx]

instance (Ï€ : Measure Bool) : IsMarkovKernel (boolKernelInv Î¼ Î½ Ï€) := by
  constructor
  intro x
  rw [boolKernelInv_apply]
  split_ifs with h
  Â· constructor
    simp [h]
  Â· infer_instance

lemma posterior_boolKernel (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    ((boolKernel Î¼ Î½)â€ Ï€) =áµ[boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€] boolKernelInv Î¼ Î½ Ï€ := by
  have h_eq := posterior_eq_withDensity Measure.absolutelyContinuous_comp_of_countable
    (Îº := boolKernel Î¼ Î½) (Î¼ := Ï€)
  have h_rnDeriv_false := Kernel.rnDeriv_eq_rnDeriv_measure (Îº := boolKernel Î¼ Î½)
    (Î· := Kernel.const Bool (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€)) (a := false)
  have h_rnDeriv_true := Kernel.rnDeriv_eq_rnDeriv_measure (Îº := boolKernel Î¼ Î½)
    (Î· := Kernel.const Bool (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€)) (a := true)
  simp only [Kernel.const_apply, boolKernel_apply, Bool.false_eq_true, â†“reduceIte]
    at h_rnDeriv_false
  simp only [Kernel.const_apply, boolKernel_apply, â†“reduceIte] at h_rnDeriv_true
  filter_upwards [h_eq, boolKernelInv_apply_false Î¼ Î½ Ï€, boolKernelInv_apply_true Î¼ Î½ Ï€,
    h_rnDeriv_false, h_rnDeriv_true] with x hx hx_false hx_true h_rnDeriv_false h_rnDeriv_true
  rw [hx]
  ext
  Â· simp only [MeasurableSpace.measurableSet_top, withDensity_apply, Measure.restrict_singleton,
      lintegral_smul_measure, lintegral_dirac, smul_eq_mul]
    rw [hx_false]
    congr!
  Â· simp only [MeasurableSpace.measurableSet_top, withDensity_apply, Measure.restrict_singleton,
      lintegral_smul_measure, lintegral_dirac, smul_eq_mul]
    rw [hx_true]
    congr!

lemma bayesRiskPrior_eq_of_hasGenBayesEstimator_binary {ğ“¨ : Type*}
    [MeasurableSpace ğ“¨] {â„“ : Bool â†’ ğ“¨ â†’ â„â‰¥0âˆ}
    (hl : Measurable (Function.uncurry â„“))
    (P : Kernel Bool ğ“§) [IsFiniteKernel P] (Ï€ : Measure Bool) [IsFiniteMeasure Ï€]
    [h : HasGenBayesEstimator â„“ P Ï€] :
    bayesRiskPrior â„“ P Ï€
      = âˆ«â» x, â¨… z, Ï€ {true} * (P true).rnDeriv (P âˆ˜â‚˜ Ï€) x * â„“ true z
        + Ï€ {false} * (P false).rnDeriv (P âˆ˜â‚˜ Ï€) x * â„“ false z âˆ‚(P âˆ˜â‚˜ Ï€) := by
  rw [bayesRiskPrior_eq_of_hasGenBayesEstimator hl]
  have h1 := posterior_boolKernel (P false) (P true) Ï€
  have h2 : P = boolKernel (P false) (P true) := Kernel_bool_eq_boolKernel P
  have h3 : (Pâ€ Ï€) = (boolKernel (P false) (P true))â€ Ï€ := by congr
  nth_rw 1 3 [h2]
  simp_rw [h3]
  apply lintegral_congr_ae
  filter_upwards [h1, boolKernelInv_apply_false (P false) (P true) Ï€,
    boolKernelInv_apply_true (P false) (P true) Ï€] with x hx h_false h_true
  congr with z
  rw [Bool.lintegral_bool, hx, h_false, h_true, â† h2]
  ring_nf

end ProbabilityTheory
