/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Lorenzo Luccioli
-/
import Mathlib.Probability.Decision.BoolKernel

/-!
# Simple Bayesian binary hypothesis testing

## Main definitions

* `binaryLoss`

## Main statements

* `fooBar_unique`

## Notation

## Implementation details

-/

open MeasureTheory

open scoped ENNReal NNReal

namespace ProbabilityTheory

@[simp]
lemma _root_.MeasureTheory.Measure.discard_comp {Î± : Type*} {_ : MeasurableSpace Î±}
    (Î¼ : Measure Î±) :
    (Kernel.discard Î±) âˆ˜â‚˜ Î¼ = Î¼ .univ â€¢ (Measure.dirac ()) := by
  ext s hs; simp [Measure.bind_apply hs (Kernel.aemeasurable _), mul_comm]

variable {Î˜ ğ“§ ğ“§' ğ“¨ : Type*} {mÎ˜ : MeasurableSpace Î˜} {mğ“§ : MeasurableSpace ğ“§}
  {mğ“§' : MeasurableSpace ğ“§'} {mğ“¨ : MeasurableSpace ğ“¨}
  {Î¼ Î½ : Measure ğ“§} {p : â„â‰¥0âˆ}

section binaryLoss

def binaryLoss [DecidableEq Î˜] : Î˜ â†’ Î˜ â†’ â„â‰¥0âˆ := fun Î¸ y â†¦ if Î¸ = y then 0 else 1

@[simp]
lemma risk_binaryLoss_true (Î¼ Î½ : Measure ğ“§) (Îº : Kernel ğ“§ Bool) :
    risk binaryLoss (boolKernel Î¼ Î½) Îº true = (Îº âˆ˜â‚˜ Î½) {false} := by
  simp only [risk, comp_boolKernel, boolKernel_apply, â†“reduceIte, binaryLoss, Bool.true_eq]
  calc âˆ«â» z, if z = true then 0 else 1 âˆ‚(Îº âˆ˜â‚˜ Î½)
  _ = âˆ«â» z, Set.indicator {false} (fun _ â†¦ 1) z âˆ‚(Îº âˆ˜â‚˜ Î½) := by
    congr with z
    rw [Set.indicator_apply]
    simp only [Set.mem_singleton_iff]
    split_ifs with h1 h2 h2
    Â· exact absurd (h2.symm.trans h1) Bool.false_ne_true
    Â· rfl
    Â· rfl
    Â· simp only [Bool.not_eq_true, Bool.not_eq_false] at h1 h2
      exact absurd (h1.symm.trans h2) Bool.false_ne_true
  _ = (Îº âˆ˜â‚˜ Î½) {false} := lintegral_indicator_one (measurableSet_singleton _)

@[simp]
lemma risk_binaryLoss_false (Î¼ Î½ : Measure ğ“§) (Îº : Kernel ğ“§ Bool) :
    risk binaryLoss (boolKernel Î¼ Î½) Îº false = (Îº âˆ˜â‚˜ Î¼) {true} := by
  simp only [risk, comp_boolKernel, boolKernel_apply, Bool.false_eq, Bool.true_eq_false, â†“reduceIte,
    binaryLoss]
  calc âˆ«â» z, if z = false then 0 else 1 âˆ‚(Îº âˆ˜â‚˜ Î¼)
  _ = âˆ«â» z, Set.indicator {true} (fun _ â†¦ 1) z âˆ‚(Îº âˆ˜â‚˜ Î¼) := by
    congr with z
    rw [Set.indicator_apply]
    classical
    simp only [Set.mem_singleton_iff]
    split_ifs with h1 h2 h2
    Â· exact absurd (h1.symm.trans h2) Bool.false_ne_true
    Â· rfl
    Â· rfl
    Â· simp at h1 h2
      exact absurd (h2.symm.trans h1) Bool.false_ne_true
  _ = (Îº âˆ˜â‚˜ Î¼) {true} := lintegral_indicator_one (measurableSet_singleton _)

/-- The function `x â†¦ ğ•€{Ï€â‚€ * âˆ‚Î¼/âˆ‚(boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x â‰¤ Ï€â‚ * âˆ‚Î½/âˆ‚(boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x}`.
It is a Generalized Bayes estimator for the simple binary hypothesis testing problem. -/
noncomputable
def binaryGenBayesEstimator (Î¼ Î½ : Measure ğ“§) (Ï€ : Measure Bool) : ğ“§ â†’ Bool :=
  let E : Set ğ“§ := {x | Ï€ {false} * Î¼.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x
    â‰¤ Ï€ {true} * Î½.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x}
  fun x â†¦ Bool.ofNat (E.indicator 1 x)

lemma binaryGenBayesEstimator_isGenBayesEstimator (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼]
    [IsFiniteMeasure Î½] (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    IsGenBayesEstimator binaryLoss (boolKernel Î¼ Î½)
      (binaryGenBayesEstimator Î¼ Î½ Ï€) Ï€ := by
  refine âŸ¨?_, ?_âŸ©
  Â· simp_rw [binaryGenBayesEstimator]
    refine Measurable.of_discrete.comp' (measurable_one.indicator (measurableSet_le ?_ ?_))
      <;> fun_prop
  Â· filter_upwards [posterior_boolKernel Î¼ Î½ Ï€, boolKernelInv_apply' Î¼ Î½ Ï€ {true},
      boolKernelInv_apply' Î¼ Î½ Ï€ {false}] with x hx h_true h_false
    refine le_antisymm (le_iInf fun b â†¦ ?_) (iInf_le _ _)
    simp only [binaryLoss, binaryGenBayesEstimator, Bool.ofNat, ne_eq,
      Set.indicator_apply_eq_zero, Set.mem_setOf_eq, Pi.ofNat_apply, one_ne_zero, imp_false,
      Bool.lintegral_bool, Bool.false_eq, decide_eq_false_iff_not, ite_mul, zero_mul,
      one_mul, Bool.true_eq, decide_eq_true_eq]
    by_cases hÏ€ : Ï€ {false} * (âˆ‚Î¼/âˆ‚boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x â‰¤ Ï€ {true} * (âˆ‚Î½/âˆ‚boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x
    Â· simp only [hÏ€, not_true_eq_false, not_false_eq_true, â†“reduceIte, add_zero]
      cases b <;> simp_all
    Â· cases b
      Â· simp_all
      Â· simp_all only [Set.mem_singleton_iff, Set.indicator_of_mem, Pi.one_apply, mul_one,
          Bool.false_eq_true, not_false_eq_true, Set.indicator_of_notMem, mul_zero, add_zero,
          Bool.true_eq_false, zero_add, not_le, not_true_eq_false, â†“reduceIte]
        gcongr

noncomputable instance (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    HasGenBayesEstimator binaryLoss (boolKernel Î¼ Î½) Ï€ :=
  âŸ¨binaryGenBayesEstimator Î¼ Î½ Ï€, binaryGenBayesEstimator_isGenBayesEstimator Î¼ Î½ Ï€âŸ©

end binaryLoss

/-- The Bayes risk of simple binary hypothesis testing with respect to a prior. -/
noncomputable
def bayesBinaryRisk (Î¼ Î½ : Measure ğ“§) (Ï€ : Measure Bool) : â„â‰¥0âˆ :=
  bayesRiskPrior binaryLoss (boolKernel Î¼ Î½) Ï€

lemma bayesBinaryRisk_eq (Î¼ Î½ : Measure ğ“§) (Ï€ : Measure Bool) :
    bayesBinaryRisk Î¼ Î½ Ï€
      = â¨… (Îº : Kernel ğ“§ Bool) (_ : IsMarkovKernel Îº),
        Ï€ {true} * (Îº âˆ˜â‚˜ Î½) {false} + Ï€ {false} * (Îº âˆ˜â‚˜ Î¼) {true} := by
  rw [bayesBinaryRisk, bayesRiskPrior]
  congr with Îº
  congr with _
  rw [bayesianRisk, lintegral_fintype, mul_comm (Ï€ {false}), mul_comm (Ï€ {true})]
  simp

variable {Ï€ : Measure Bool}

/-- `B (aâ€¢Î¼, bâ€¢Î½; Ï€) = B (Î¼, Î½; (a*Ï€â‚€, b*Ï€â‚)).` -/
lemma bayesBinaryRisk_smul_smul (Î¼ Î½ : Measure ğ“§) (Ï€ : Measure Bool) (a b : â„â‰¥0âˆ) :
    bayesBinaryRisk (a â€¢ Î¼) (b â€¢ Î½) Ï€
      = bayesBinaryRisk Î¼ Î½ (Ï€.withDensity (fun x â†¦ bif x then b else a)) := by
  simp [bayesBinaryRisk_eq, Measure.comp_smul, lintegral_dirac, mul_assoc]

lemma bayesBinaryRisk_eq_bayesBinaryRisk_one_one (Î¼ Î½ : Measure ğ“§) (Ï€ : Measure Bool) :
    bayesBinaryRisk Î¼ Î½ Ï€
      = bayesBinaryRisk (Ï€ {false} â€¢ Î¼) (Ï€ {true} â€¢ Î½) (Bool.boolMeasure 1 1) := by
  rw [bayesBinaryRisk_smul_smul, Bool.measure_eq_boolMeasure Ï€, Bool.boolMeasure_withDensity]
  simp

/-- **Data processing inequality** for the Bayes binary risk. -/
lemma bayesBinaryRisk_le_bayesBinaryRisk_comp (Î¼ Î½ : Measure ğ“§) (Ï€ : Measure Bool)
    (Î· : Kernel ğ“§ ğ“§') [IsMarkovKernel Î·] :
    bayesBinaryRisk Î¼ Î½ Ï€ â‰¤ bayesBinaryRisk (Î· âˆ˜â‚˜ Î¼) (Î· âˆ˜â‚˜ Î½) Ï€ :=
  (bayesRiskPrior_le_bayesRiskPrior_comp _ _ _ Î·).trans_eq (by simp [bayesBinaryRisk])

lemma nonempty_subtype_isMarkovKernel_of_nonempty {ğ“§ : Type*} {mğ“§ : MeasurableSpace ğ“§}
    {ğ“¨ : Type*} {mğ“¨ : MeasurableSpace ğ“¨} [Nonempty ğ“¨] :
    Nonempty (Subtype (@IsMarkovKernel ğ“§ ğ“¨ mğ“§ mğ“¨)) := by
  simp only [nonempty_subtype]
  let y : ğ“¨ := Classical.ofNonempty
  exact âŸ¨Kernel.const _ (Measure.dirac y), inferInstanceâŸ©

@[simp]
lemma bayesBinaryRisk_self (Î¼ : Measure ğ“§) (Ï€ : Measure Bool) :
    bayesBinaryRisk Î¼ Î¼ Ï€ = min (Ï€ {false}) (Ï€ {true}) * Î¼ .univ := by
  rw [bayesBinaryRisk_eq]
  refine le_antisymm ?_ ?_
  Â· let Î· : Kernel ğ“§ Bool :=
      if Ï€ {true} â‰¤ Ï€ {false} then (Kernel.const ğ“§ (Measure.dirac false))
        else (Kernel.const ğ“§ (Measure.dirac true))
    refine iInf_le_of_le Î· ?_
    simp_rw [Î·]
    convert iInf_le _ ?_ using 1
    Â· split_ifs with h <;> simp [le_of_not_ge, h]
    Â· split_ifs <;> infer_instance
  Â· calc
      _ â‰¥ â¨… Îº, â¨… (_ : IsMarkovKernel Îº), min (Ï€ {false}) (Ï€ {true}) * (Îº âˆ˜â‚˜ Î¼) {false}
          + min (Ï€ {false}) (Ï€ {true}) * (Îº âˆ˜â‚˜ Î¼) {true} := by
        gcongr <;> simp
      _ = â¨… Îº, â¨… (_ : IsMarkovKernel Îº), min (Ï€ {false}) (Ï€ {true}) * Î¼ .univ := by
        simp_rw [â† mul_add, â† measure_union (show Disjoint {false} {true} from by simp)
          (by trivial), (set_fintype_card_eq_univ_iff ({false} âˆª {true})).mp rfl,
          Measure.comp_apply_univ]
        rfl
      _ = _ := by
        rw [iInf_subtype']
        convert iInf_const
        exact nonempty_subtype_isMarkovKernel_of_nonempty

lemma bayesBinaryRisk_dirac (a b : â„â‰¥0âˆ) (x : ğ“§) (Ï€ : Measure Bool) :
    bayesBinaryRisk (a â€¢ Measure.dirac x) (b â€¢ Measure.dirac x) Ï€
      = min (Ï€ {false} * a) (Ï€ {true} * b) := by
  rw [bayesBinaryRisk_smul_smul, bayesBinaryRisk_self]
  simp [lintegral_dirac]

lemma bayesBinaryRisk_le_min (Î¼ Î½ : Measure ğ“§) (Ï€ : Measure Bool) :
    bayesBinaryRisk Î¼ Î½ Ï€ â‰¤ min (Ï€ {false} * Î¼ .univ) (Ï€ {true} * Î½ .univ) := by
  convert bayesBinaryRisk_le_bayesBinaryRisk_comp Î¼ Î½ Ï€ (Kernel.discard ğ“§)
  simp_rw [Measure.discard_comp, bayesBinaryRisk_dirac]

@[simp] lemma bayesBinaryRisk_zero_left : bayesBinaryRisk 0 Î½ Ï€ = 0 :=
  le_antisymm ((bayesBinaryRisk_le_min _ _ _).trans (by simp)) zero_le'

@[simp] lemma bayesBinaryRisk_zero_right : bayesBinaryRisk Î¼ 0 Ï€ = 0 :=
  le_antisymm ((bayesBinaryRisk_le_min _ _ _).trans (by simp)) zero_le'

@[simp] lemma bayesBinaryRisk_zero_prior : bayesBinaryRisk Î¼ Î½ 0 = 0 :=
  le_antisymm ((bayesBinaryRisk_le_min _ _ _).trans (by simp)) zero_le'

lemma bayesBinaryRisk_ne_top (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    bayesBinaryRisk Î¼ Î½ Ï€ â‰  âˆ := by
  refine lt_top_iff_ne_top.mp ((bayesBinaryRisk_le_min Î¼ Î½ Ï€).trans_lt ?_)
  exact min_lt_iff.mpr <| Or.inl <| ENNReal.mul_lt_top (measure_lt_top Ï€ _) (measure_lt_top Î¼ _)

lemma bayesBinaryRisk_of_measure_true_eq_zero (Î¼ Î½ : Measure ğ“§) (hÏ€ : Ï€ {true} = 0) :
    bayesBinaryRisk Î¼ Î½ Ï€ = 0 := by
  refine le_antisymm ?_ (zero_le _)
  convert bayesBinaryRisk_le_min Î¼ Î½ Ï€
  simp [hÏ€]

lemma bayesBinaryRisk_of_measure_false_eq_zero (Î¼ Î½ : Measure ğ“§) (hÏ€ : Ï€ {false} = 0) :
    bayesBinaryRisk Î¼ Î½ Ï€ = 0 := by
  refine le_antisymm ?_ (zero_le _)
  convert bayesBinaryRisk_le_min Î¼ Î½ Ï€
  simp [hÏ€]

lemma bayesBinaryRisk_symm (Î¼ Î½ : Measure ğ“§) (Ï€ : Measure Bool) :
    bayesBinaryRisk Î¼ Î½ Ï€ = bayesBinaryRisk Î½ Î¼ (Ï€.map Bool.not) := by
  have : (Bool.not â»Â¹' {true}) = {false} := by ext x; simp
  have h1 : (Ï€.map Bool.not) {true} = Ï€ {false} := by
    rw [Measure.map_apply (by exact fun _ a â†¦ a) (by trivial), this]
  have : (Bool.not â»Â¹' {false}) = {true} := by ext x; simp
  have h2 : (Ï€.map Bool.not) {false} = Ï€ {true} := by
    rw [Measure.map_apply (by exact fun _ a â†¦ a) (by trivial), this]
  simp_rw [bayesBinaryRisk_eq, h1, h2, add_comm, iInf_subtype']
  -- from this point on the proof is basically a change of variable inside the iInf,
  -- to do this I define an equivalence between `Subtype IsMarkovKernel` and itself through
  -- the `Bool.not` operation, maybe it can be shortened or something can be separated as
  -- a different lemma, but I'm not sure how useful this would be
  let e : (Kernel ğ“§ Bool) â‰ƒ (Kernel ğ“§ Bool) := by
    have h_id : (Kernel.deterministic Bool.not (fun _ a â†¦ a)).comap Bool.not (fun _ a â†¦ a)
        = Kernel.id := by
      ext x : 1
      simp_rw [Kernel.comap_apply, Kernel.deterministic_apply, Kernel.id_apply, Bool.not_not]
    refine âŸ¨fun Îº â†¦ (Kernel.deterministic Bool.not (fun _ a â†¦ a)) âˆ˜â‚– Îº,
      fun Îº â†¦ (Kernel.deterministic Bool.not (fun _ a â†¦ a)) âˆ˜â‚– Îº, fun Îº â†¦ ?_, fun Îº â†¦ ?_âŸ© <;>
    Â· dsimp
      ext x : 1
      rw [â† Kernel.comp_assoc, Kernel.comp_deterministic_eq_comap, h_id, Kernel.id_comp]
  let e' : (Subtype (@IsMarkovKernel ğ“§ Bool _ _)) â‰ƒ (Subtype (@IsMarkovKernel ğ“§ Bool _ _)) := by
    refine âŸ¨fun âŸ¨Îº, _âŸ© â†¦ âŸ¨e Îº, ?_âŸ©, fun âŸ¨Îº, _âŸ© â†¦ âŸ¨e.symm Îº, ?_âŸ©, fun Îº â†¦ by simp, fun Îº â†¦ by simpâŸ©
      <;> simp only [Equiv.coe_fn_mk, Equiv.coe_fn_symm_mk, e] <;> infer_instance
  rw [â† Equiv.iInf_comp e']
  congr with Îº
  simp only [Equiv.coe_fn_mk, Equiv.coe_fn_symm_mk, e', e]
  have h3 b : Set.indicator {true} (1 : Bool â†’ â„â‰¥0âˆ) b.not = Set.indicator {false} 1 b := by
    cases b <;> simp
  have h4 b : Set.indicator {false} (1 : Bool â†’ â„â‰¥0âˆ) b.not = Set.indicator {true} 1 b := by
    cases b <;> simp
  congr 2 <;>
  Â· rw [Measure.bind_apply (by trivial) (Kernel.aemeasurable _),
      Measure.bind_apply (by trivial) (Kernel.aemeasurable _)]
    congr with x
    rw [Kernel.comp_apply']
    simp only [Measure.dirac_apply' _ (show MeasurableSet {true} by trivial),
      Measure.dirac_apply' _ (show MeasurableSet {false} by trivial), Kernel.deterministic_apply]
    swap; trivial
    simp [h3, h4]

lemma bayesianRisk_binary_of_deterministic_indicator (Î¼ Î½ : Measure ğ“§) (Ï€ : Measure Bool)
    {E : Set ğ“§} (hE : MeasurableSet E) :
    bayesianRisk binaryLoss (boolKernel Î¼ Î½)
      (Kernel.deterministic (fun x â†¦ Bool.ofNat (E.indicator 1 x))
        (Measurable.of_discrete.comp' (measurable_one.indicator hE))) Ï€
      = Ï€ {false} * Î¼ E + Ï€ {true} * Î½ Eá¶œ := by
  have h_meas : Measurable fun x â†¦ Bool.ofNat (E.indicator 1 x) :=
    Measurable.of_discrete.comp' (measurable_one.indicator hE)
  have h1 : (fun x â†¦ Bool.ofNat (E.indicator 1 x)) â»Â¹' {false} = Eá¶œ := by
    ext; simp [Bool.ofNat]
  have h2 : (fun x â†¦ Bool.ofNat (E.indicator 1 x)) â»Â¹' {true} = E := by
    ext; simp [Bool.ofNat]
  rw [bayesianRisk, Bool.lintegral_bool, mul_comm (Ï€ {false}), mul_comm (Ï€ {true})]
  simp only [risk_binaryLoss_false, risk_binaryLoss_true]
  simp_rw [Measure.deterministic_comp_eq_map, Measure.map_apply h_meas trivial, h1, h2]

lemma bayesBinaryRisk_eq_iInf_measurableSet (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼]
    [IsFiniteMeasure Î½] (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    bayesBinaryRisk Î¼ Î½ Ï€ = â¨… E, â¨… (_ : MeasurableSet E), Ï€ {false} * Î¼ E + Ï€ {true} * Î½ Eá¶œ := by
  apply le_antisymm
  Â· simp_rw [le_iInf_iff, bayesBinaryRisk, bayesRiskPrior]
    intro E hE
    rw [â† bayesianRisk_binary_of_deterministic_indicator _ _ _ hE]
    exact iInf_le_of_le _ (iInf_le _ (Kernel.isMarkovKernel_deterministic _))
  Â· let E := {x | Ï€ {false} * (âˆ‚Î¼/âˆ‚boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x
      â‰¤ Ï€ {true} * (âˆ‚Î½/âˆ‚boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x}
    have hE : MeasurableSet E := measurableSet_le (by fun_prop) (by fun_prop)
    rw [bayesBinaryRisk, â† IsGenBayesEstimator.isBayesEstimator
      (binaryGenBayesEstimator_isGenBayesEstimator Î¼ Î½ Ï€) .of_discrete, IsGenBayesEstimator.kernel]
    simp_rw [binaryGenBayesEstimator]
    rw [bayesianRisk_binary_of_deterministic_indicator _ _ _ hE]
    exact iInf_le_of_le E (iInf_le _ hE)

lemma bayesBinaryRisk_eq_lintegral_min (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼]
    [IsFiniteMeasure Î½] (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    bayesBinaryRisk Î¼ Î½ Ï€ = âˆ«â» x, min (Ï€ {false} * Î¼.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x)
      (Ï€ {true} * Î½.rnDeriv (boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x) âˆ‚(boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) := by
  simp [bayesBinaryRisk, bayesRiskPrior_eq_of_hasGenBayesEstimator_binary .of_discrete,
    iInf_bool_eq, binaryLoss]

end ProbabilityTheory
