/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Lorenzo Luccioli
-/
module

public import Mathlib.Probability.Decision.BayesEstimator
public import Mathlib.Probability.Decision.BoolMeasure
public import Mathlib.Probability.Decision.Risk.Countable

/-!
# Simple Bayesian binary hypothesis testing

## Main definitions

* `binaryLoss`

## Main statements

* `fooBar_unique`

-/

@[expose] public section

open MeasureTheory

open scoped ENNReal NNReal

section

variable {Î± : Type*} [Field Î±] [LinearOrder Î±] [IsStrictOrderedRing Î±]

lemma max_eq_add_add_abs_sub (a b : Î±) : max a b = 2â»Â¹ * (a + b + |a - b|) := by
  rw [â† max_add_min a, â† max_sub_min_eq_abs', add_sub_left_comm, add_sub_cancel_right]
  ring

lemma min_eq_add_sub_abs_sub (a b : Î±) : min a b = 2â»Â¹ * (a + b - |a - b|) := by
  rw [â† min_add_max a, â† max_sub_min_eq_abs', add_sub_assoc, sub_sub_cancel]
  ring

end

namespace ProbabilityTheory

variable {Î˜ ğ“§ ğ“§' ğ“¨ : Type*} {mÎ˜ : MeasurableSpace Î˜} {mğ“§ : MeasurableSpace ğ“§}
  {mğ“§' : MeasurableSpace ğ“§'} {mğ“¨ : MeasurableSpace ğ“¨}
  {Î¼ Î½ : Measure ğ“§} {p : â„â‰¥0âˆ} {Ï€ : Measure Bool}

section BinaryLoss

/-- The binary loss function, which is 0 if the two arguments are equal and 1 otherwise. -/
def binaryLoss [DecidableEq Î˜] : Î˜ â†’ Î˜ â†’ â„â‰¥0âˆ := fun Î¸ y â†¦ if Î¸ = y then 0 else 1

@[simp]
lemma integral_binaryLoss_true (Î½ : Measure Bool) : âˆ«â» y, binaryLoss true y âˆ‚Î½ = Î½ {false} := by
  simp [binaryLoss, lintegral_bool]

@[simp]
lemma integral_binaryLoss_false (Î½ : Measure Bool) : âˆ«â» y, binaryLoss false y âˆ‚Î½ = Î½ {true} := by
  simp [binaryLoss, lintegral_bool]

instance (P : Kernel Bool ğ“§) [IsFiniteKernel P] (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    HasGenBayesEstimator binaryLoss P Ï€ :=
  hasGenBayesEstimator_of_finite (by fun_prop)

end BinaryLoss

section BinaryBayesEstimator

/-! ### Explicit form for the Bayes estimator. -/

/-- The function `x â†¦ ğ•€{Ï€â‚€ * âˆ‚Î¼/âˆ‚(boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x â‰¤ Ï€â‚ * âˆ‚Î½/âˆ‚(boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x}`.
It is a generalized Bayes estimator for the simple binary hypothesis testing problem. -/
noncomputable
def binaryBayesEstimator (Î¼ Î½ : Measure ğ“§) (Ï€ : Measure Bool) : ğ“§ â†’ Bool :=
  fun x â†¦ (Ï€ {false} * Î¼.rnDeriv (Kernel.boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x
    â‰¤ Ï€ {true} * Î½.rnDeriv (Kernel.boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x)

lemma binaryBayesEstimator_eq :
    binaryBayesEstimator Î¼ Î½ Ï€ =
      let E : Set ğ“§ := {x | Ï€ {false} * Î¼.rnDeriv (Kernel.boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x
        â‰¤ Ï€ {true} * Î½.rnDeriv (Kernel.boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x}
      fun x â†¦ Bool.ofNat (E.indicator 1 x) := by
  unfold binaryBayesEstimator
  ext x
  simp [Bool.ofNat]

@[fun_prop]
lemma measurable_binaryBayesEstimator : Measurable (binaryBayesEstimator Î¼ Î½ Ï€) :=
  Measurable.ite (measurableSet_le (by fun_prop) (by fun_prop)) (by fun_prop) (by fun_prop)

lemma isGenBayesEstimator_binaryBayesEstimator (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼]
    [IsFiniteMeasure Î½] (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    IsGenBayesEstimator binaryLoss (Kernel.boolKernel Î¼ Î½) (binaryBayesEstimator Î¼ Î½ Ï€) Ï€ := by
  refine âŸ¨by fun_prop, ?_âŸ©
  simp only [binaryLoss, lintegral_bool, Bool.false_eq, ite_mul, zero_mul, one_mul,
    Bool.true_eq]
  filter_upwards [posterior_boolKernel_apply_true Î¼ Î½ Ï€,
    posterior_boolKernel_apply_false Î¼ Î½ Ï€] with x h_true h_false
  refine le_antisymm (le_iInf fun b â†¦ ?_) (iInf_le _ _)
  simp only [binaryBayesEstimator, decide_eq_false_iff_not, not_le, h_false, decide_eq_true_eq,
    h_true]
  by_cases hÏ€ : Ï€ {false} * (âˆ‚Î¼/âˆ‚Kernel.boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x
      â‰¤ Ï€ {true} * (âˆ‚Î½/âˆ‚Kernel.boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x
    <;> cases b
  Â· simp [hÏ€, not_lt.mpr hÏ€]
  Â· simp [hÏ€, not_lt.mpr hÏ€]
  Â· simp [hÏ€, not_le.mp hÏ€]
  Â· simpa [hÏ€, not_le.mp hÏ€] using (not_le.mp hÏ€).le

end BinaryBayesEstimator

/-- The Bayes risk of simple binary hypothesis testing with respect to a prior. -/
noncomputable
def bayesBinaryRisk (Î¼ Î½ : Measure ğ“§) (Ï€ : Measure Bool) : â„â‰¥0âˆ :=
  bayesRisk binaryLoss (Kernel.boolKernel Î¼ Î½) Ï€

lemma bayesBinaryRisk_eq (Î¼ Î½ : Measure ğ“§) (Ï€ : Measure Bool) :
    bayesBinaryRisk Î¼ Î½ Ï€
      = â¨… (Îº : Kernel ğ“§ Bool) (_ : IsMarkovKernel Îº),
        Ï€ {true} * (Îº âˆ˜â‚˜ Î½) {false} + Ï€ {false} * (Îº âˆ˜â‚˜ Î¼) {true} := by
  simp [bayesBinaryRisk, bayesRisk_fintype, mul_comm]

variable {Ï€ : Measure Bool}

/-- `B (aâ€¢Î¼, bâ€¢Î½; Ï€) = B (Î¼, Î½; (a*Ï€â‚€, b*Ï€â‚)).` -/
lemma bayesBinaryRisk_smul_smul (Î¼ Î½ : Measure ğ“§) (Ï€ : Measure Bool) (a b : â„â‰¥0âˆ) :
    bayesBinaryRisk (a â€¢ Î¼) (b â€¢ Î½) Ï€
      = bayesBinaryRisk Î¼ Î½ (Ï€.withDensity (fun x â†¦ if x then b else a)) := by
  simp [bayesBinaryRisk_eq, Measure.comp_smul, lintegral_dirac, mul_assoc]

/-- **Data processing inequality** for the Bayes binary risk. -/
lemma bayesBinaryRisk_le_bayesBinaryRisk_comp (Î¼ Î½ : Measure ğ“§) (Ï€ : Measure Bool)
    (Î· : Kernel ğ“§ ğ“§') [IsMarkovKernel Î·] :
    bayesBinaryRisk Î¼ Î½ Ï€ â‰¤ bayesBinaryRisk (Î· âˆ˜â‚˜ Î¼) (Î· âˆ˜â‚˜ Î½) Ï€ :=
  (bayesRisk_le_bayesRisk_comp _ _ _ Î·).trans_eq (by simp [bayesBinaryRisk])

@[simp]
lemma bayesBinaryRisk_self (Î¼ : Measure ğ“§) (Ï€ : Measure Bool) :
    bayesBinaryRisk Î¼ Î¼ Ï€ = min (Ï€ {false}) (Ï€ {true}) * Î¼ .univ := by
  have : Kernel.boolKernel Î¼ Î¼ = Kernel.const Bool Î¼ := by ext; simp
  rw [bayesBinaryRisk, mul_comm, mul_min, this,
    bayesRisk_const_of_fintype (by fun_prop)]
  simp [lintegral_bool, binaryLoss, iInf_bool_eq]

lemma bayesBinaryRisk_dirac (a b : â„â‰¥0âˆ) (x : ğ“§) (Ï€ : Measure Bool) :
    bayesBinaryRisk (a â€¢ Measure.dirac x) (b â€¢ Measure.dirac x) Ï€
      = min (Ï€ {false} * a) (Ï€ {true} * b) := by
  simp [bayesBinaryRisk_smul_smul]

lemma bayesBinaryRisk_le_min (Î¼ Î½ : Measure ğ“§) (Ï€ : Measure Bool) :
    bayesBinaryRisk Î¼ Î½ Ï€ â‰¤ min (Ï€ {false} * Î¼ .univ) (Ï€ {true} * Î½ .univ) := by
  refine (bayesBinaryRisk_le_bayesBinaryRisk_comp Î¼ Î½ Ï€ (Kernel.discard ğ“§)).trans_eq ?_
  simp_rw [Measure.discard_comp, bayesBinaryRisk_dirac]

@[simp] lemma bayesBinaryRisk_zero_left : bayesBinaryRisk 0 Î½ Ï€ = 0 :=
  le_antisymm ((bayesBinaryRisk_le_min _ _ _).trans (by simp)) zero_le'

@[simp] lemma bayesBinaryRisk_zero_right : bayesBinaryRisk Î¼ 0 Ï€ = 0 :=
  le_antisymm ((bayesBinaryRisk_le_min _ _ _).trans (by simp)) zero_le'

@[simp] lemma bayesBinaryRisk_zero_prior : bayesBinaryRisk Î¼ Î½ 0 = 0 := by
  simp [bayesBinaryRisk]

lemma bayesBinaryRisk_ne_top (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼]
    (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    bayesBinaryRisk Î¼ Î½ Ï€ â‰  âˆ := by
  refine lt_top_iff_ne_top.mp ((bayesBinaryRisk_le_min Î¼ Î½ Ï€).trans_lt ?_)
  exact min_lt_iff.mpr <| Or.inl <| ENNReal.mul_lt_top (measure_lt_top Ï€ _) (measure_lt_top Î¼ _)

lemma bayesBinaryRisk_of_measure_true_eq_zero (Î¼ Î½ : Measure ğ“§) (hÏ€ : Ï€ {true} = 0) :
    bayesBinaryRisk Î¼ Î½ Ï€ = 0 :=
  le_antisymm ((bayesBinaryRisk_le_min Î¼ Î½ Ï€).trans (by simp [hÏ€])) zero_le'

lemma bayesBinaryRisk_of_measure_false_eq_zero (Î¼ Î½ : Measure ğ“§) (hÏ€ : Ï€ {false} = 0) :
    bayesBinaryRisk Î¼ Î½ Ï€ = 0 :=
  le_antisymm ((bayesBinaryRisk_le_min Î¼ Î½ Ï€).trans (by simp [hÏ€])) zero_le'

lemma bayesBinaryRisk_comm (Î¼ Î½ : Measure ğ“§) (Ï€ : Measure Bool) :
    bayesBinaryRisk Î¼ Î½ Ï€ = bayesBinaryRisk Î½ Î¼ (Ï€.map Bool.not) := by
  simp only [bayesBinaryRisk_eq, Measure.map_not_apply_true, Measure.map_not_apply_false]
  simp_rw [add_comm, iInf_subtype']
  -- from this point on the proof is basically a change of variable inside the iInf,
  -- to do this I define an equivalence between `Subtype IsMarkovKernel` and itself through
  -- the `Bool.not` operation, maybe it can be shortened or something can be separated as
  -- a different lemma, but I'm not sure how useful this would be
  have : Bool.not âˆ˜ Bool.not = id := by ext; simp [Bool.not_not]
  let e : (Kernel ğ“§ Bool) â‰ƒ (Kernel ğ“§ Bool) := by
    refine âŸ¨fun Îº â†¦ Îº.map Bool.not, fun Îº â†¦ Îº.map Bool.not, fun Îº â†¦ ?_, fun Îº â†¦ ?_âŸ© <;>
    Â· simp only
      rw [â† Kernel.map_comp_right _ (by fun_prop) (by fun_prop), this, Kernel.map_id]
  let e' : (Subtype (@IsMarkovKernel ğ“§ Bool _ _)) â‰ƒ (Subtype (@IsMarkovKernel ğ“§ Bool _ _)) := by
    refine âŸ¨fun âŸ¨Îº, _âŸ© â†¦ âŸ¨e Îº, ?_âŸ©, fun âŸ¨Îº, _âŸ© â†¦ âŸ¨e.symm Îº, ?_âŸ©, fun Îº â†¦ by simp, fun Îº â†¦ by simpâŸ©
      <;> simp only [Equiv.coe_fn_mk, Equiv.coe_fn_symm_mk, e]
      <;> exact Kernel.IsMarkovKernel.map Îº (by fun_prop)
  rw [â† Equiv.iInf_comp e']
  congr with Îº
  simp only [Equiv.coe_fn_mk, Equiv.coe_fn_symm_mk, e', e]
  congr 2 <;>
  Â· rw [Measure.bind_apply (by trivial) (Kernel.aemeasurable _),
      Measure.bind_apply (by trivial) (Kernel.aemeasurable _)]
    congr with x
    rw [Kernel.map_apply' _ (by fun_prop)]
    Â· simp
    Â· exact measurableSet_singleton _

lemma bayesBinaryRisk_eq_bayesBinaryRisk_one_one (Î¼ Î½ : Measure ğ“§) (Ï€ : Measure Bool) :
    bayesBinaryRisk Î¼ Î½ Ï€
      = bayesBinaryRisk (Ï€ {false} â€¢ Î¼) (Ï€ {true} â€¢ Î½) (boolMeasure 1 1) := by
  rw [bayesBinaryRisk_smul_smul, measure_eq_boolMeasure Ï€, withDensity_eq_boolMeasure]
  simp

lemma avgRisk_binary_of_deterministic_indicator (Î¼ Î½ : Measure ğ“§) (Ï€ : Measure Bool)
    {E : Set ğ“§} (hE : MeasurableSet E) :
    avgRisk binaryLoss (Kernel.boolKernel Î¼ Î½)
      (Kernel.deterministic (fun x â†¦ Bool.ofNat (E.indicator 1 x))
        (Measurable.of_discrete.comp' (measurable_one.indicator hE))) Ï€
      = Ï€ {false} * Î¼ E + Ï€ {true} * Î½ Eá¶œ := by
  have h_meas : Measurable fun x â†¦ Bool.ofNat (E.indicator 1 x) :=
    Measurable.of_discrete.comp' (measurable_one.indicator hE)
  have h1 : (fun x â†¦ Bool.ofNat (E.indicator 1 x)) â»Â¹' {false} = Eá¶œ := by ext; simp [Bool.ofNat]
  have h2 : (fun x â†¦ Bool.ofNat (E.indicator 1 x)) â»Â¹' {true} = E := by ext; simp [Bool.ofNat]
  rw [avgRisk, lintegral_bool, mul_comm (Ï€ {false}), mul_comm (Ï€ {true})]
  simp only [Kernel.comp_boolKernel, Kernel.boolKernel_apply, Bool.false_eq_true, â†“reduceIte,
    integral_binaryLoss_false, integral_binaryLoss_true]
  simp_rw [Measure.deterministic_comp_eq_map, Measure.map_apply h_meas trivial, h1, h2]

lemma bayesBinaryRisk_eq_iInf_measurableSet (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼]
    [IsFiniteMeasure Î½] (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    bayesBinaryRisk Î¼ Î½ Ï€ = â¨… (E) (_ : MeasurableSet E), Ï€ {false} * Î¼ E + Ï€ {true} * Î½ Eá¶œ := by
  apply le_antisymm
  Â· simp_rw [le_iInf_iff, bayesBinaryRisk, bayesRisk]
    intro E hE
    rw [â† avgRisk_binary_of_deterministic_indicator _ _ _ hE]
    exact iInf_le_of_le _ (iInf_le _ (Kernel.isMarkovKernel_deterministic _))
  Â· let E := {x | Ï€ {false} * (âˆ‚Î¼/âˆ‚Kernel.boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x
      â‰¤ Ï€ {true} * (âˆ‚Î½/âˆ‚Kernel.boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x}
    have hE : MeasurableSet E := measurableSet_le (by fun_prop) (by fun_prop)
    rw [bayesBinaryRisk, â† IsGenBayesEstimator.isBayesEstimator
      (isGenBayesEstimator_binaryBayesEstimator Î¼ Î½ Ï€) .of_discrete, IsGenBayesEstimator.kernel]
    simp_rw [binaryBayesEstimator_eq]
    rw [avgRisk_binary_of_deterministic_indicator _ _ _ hE]
    exact iInf_le_of_le E (iInf_le _ hE)

lemma bayesRisk_eq_of_hasGenBayesEstimator_binary {ğ“¨ : Type*} [MeasurableSpace ğ“¨]
    {â„“ : Bool â†’ ğ“¨ â†’ â„â‰¥0âˆ} (hl : Measurable (Function.uncurry â„“))
    (P : Kernel Bool ğ“§) [IsFiniteKernel P] (Ï€ : Measure Bool) [IsFiniteMeasure Ï€]
    [h : HasGenBayesEstimator â„“ P Ï€] :
    bayesRisk â„“ P Ï€
      = âˆ«â» x, â¨… z, Ï€ {true} * (P true).rnDeriv (P âˆ˜â‚˜ Ï€) x * â„“ true z
        + Ï€ {false} * (P false).rnDeriv (P âˆ˜â‚˜ Ï€) x * â„“ false z âˆ‚(P âˆ˜â‚˜ Ï€) := by
  rw [bayesRisk_eq_of_hasGenBayesEstimator hl]
  have h2 : P = Kernel.boolKernel (P false) (P true) := Kernel.eq_boolKernel P
  have h3 : (Pâ€ Ï€) = (Kernel.boolKernel (P false) (P true))â€ Ï€ := by congr
  nth_rw 1 3 [h2]
  simp_rw [h3]
  apply lintegral_congr_ae
  filter_upwards [posterior_boolKernel_apply_false (P false) (P true) Ï€,
    posterior_boolKernel_apply_true (P false) (P true) Ï€] with x h_false h_true
  congr with z
  rw [lintegral_bool, h_false, h_true, â† h2]
  ring_nf

lemma bayesBinaryRisk_eq_lintegral_min (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼]
    [IsFiniteMeasure Î½] (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    bayesBinaryRisk Î¼ Î½ Ï€ = âˆ«â» x, min (Ï€ {false} * Î¼.rnDeriv (Kernel.boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x)
      (Ï€ {true} * Î½.rnDeriv (Kernel.boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x) âˆ‚(Kernel.boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) := by
  simp [bayesBinaryRisk, bayesRisk_eq_of_hasGenBayesEstimator_binary .of_discrete,
    iInf_bool_eq, binaryLoss]

lemma ENNReal.ofReal_min {a b : â„} : ENNReal.ofReal (min a b) = min (.ofReal a) (.ofReal b) := by
  wlog hab : a â‰¤ b
  Â· rw [min_comm a, min_comm (ENNReal.ofReal a)]
    exact this (by linarith)
  rw [min_eq_left hab, min_eq_left]
  exact ENNReal.ofReal_le_ofReal hab

lemma toReal_bayesBinaryRisk_eq_integral_min (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼]
    [IsFiniteMeasure Î½] (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    (bayesBinaryRisk Î¼ Î½ Ï€).toReal
      = âˆ« x, min (Ï€.real {false} * (Î¼.rnDeriv (Kernel.boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x).toReal)
        (Ï€.real {true} * (Î½.rnDeriv (Kernel.boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x).toReal)
        âˆ‚(Kernel.boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) := by
  rw [bayesBinaryRisk_eq_lintegral_min, integral_eq_lintegral_of_nonneg_ae]
  rotate_left
  Â· filter_upwards with x; positivity
  Â· fun_prop
  congr 1
  apply lintegral_congr_ae
  filter_upwards [Î¼.rnDeriv_ne_top _, Î½.rnDeriv_ne_top _] with x hxÎ¼ hxÎ½
  rw [ENNReal.ofReal_min, ENNReal.ofReal_mul (by positivity), ENNReal.ofReal_mul (by positivity),
    ofReal_measureReal, ofReal_measureReal, ENNReal.ofReal_toReal (by finiteness),
    ENNReal.ofReal_toReal (by finiteness)]

@[fun_prop]
lemma integrable_toReal_rnDeriv [IsFiniteMeasure Î¼] :
    Integrable (fun x â†¦ (Î¼.rnDeriv Î½ x).toReal) Î½ :=
  integrable_toReal_of_lintegral_ne_top (by fun_prop)
    (Measure.lintegral_rnDeriv_le.trans_lt (by simp)).ne

lemma toReal_bayesBinaryRisk_eq_integral_abs (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼]
    [IsFiniteMeasure Î½] (Ï€ : Measure Bool) [IsFiniteMeasure Ï€] :
    (bayesBinaryRisk Î¼ Î½ Ï€).toReal
      = 2â»Â¹ * (Ï€.real {false} * Î¼.real .univ + Ï€.real {true} * Î½.real .univ
        - âˆ« x, |Ï€.real {false} * (Î¼.rnDeriv (Kernel.boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x).toReal
          - Ï€.real {true} * (Î½.rnDeriv (Kernel.boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) x).toReal|
          âˆ‚(Kernel.boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€)) := by
  simp_rw [toReal_bayesBinaryRisk_eq_integral_min, min_eq_add_sub_abs_sub, integral_const_mul]
  congr
  rw [integral_sub (by fun_prop) (by fun_prop), integral_add (by fun_prop) (by fun_prop)]
  simp only [Measure.real, sub_left_inj, integral_const_mul]
  calc
    _ = (Ï€ {false}).toReal * (Î¼ .univ).toReal + (Ï€ {true}).toReal
        * âˆ« (a : ğ“§), ((âˆ‚Î½/âˆ‚Kernel.boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) a).toReal âˆ‚(Kernel.boolKernel Î¼ Î½ âˆ˜â‚˜ Ï€) := by
      by_cases hÏ€_false : Ï€ {false} = 0
      Â· simp [hÏ€_false]
      rw [Measure.integral_toReal_rnDeriv (absolutelyContinuous_boolKernel_comp_left Î¼ Î½ hÏ€_false)]
      rfl
    _ = (Ï€ {false}).toReal * (Î¼ .univ).toReal + (Ï€ {true}).toReal * (Î½ .univ).toReal := by
      by_cases hÏ€_true : Ï€ {true} = 0
      Â· simp [hÏ€_true]
      rw [Measure.integral_toReal_rnDeriv (absolutelyContinuous_boolKernel_comp_right Î¼ Î½ hÏ€_true)]
      rfl

end ProbabilityTheory
