/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Lorenzo Luccioli
-/
module

public import Mathlib.Probability.Decision.AuxLemmas
public import Mathlib.Probability.Decision.Risk.Basic
public import Mathlib.Probability.Kernel.Posterior

/-!
# Bayes estimator and generalized Bayes estimator

## Main definitions

* `IsBayesEstimator`: an estimator is a Bayes estimator if it attains the Bayes risk for the prior.
* `IsGenBayesEstimator`: a measurable function `f : ğ“§ â†’ ğ“¨` is a generalized Bayes estimator
  with respect to the prior `Ï€` if for `(P âˆ˜â‚˜ Ï€)`-almost every `x` it has
  the form `x â†¦ argmin_y Pâ€ Ï€(x)[Î¸ â†¦ â„“ Î¸ y]`.
* `HasGenBayesEstimator`: class that states that estimation problem admits a generalized Bayes
  estimator with respect to the prior.

## Main statements

* `lintegral_iInf_posterior_le_bayesRisk`: the Bayes risk with respect to a prior is bounded
  from below by the integral over the data (with distribution `P âˆ˜â‚˜ Ï€`) of the infimum over the
  possible predictions `y` of the posterior loss `âˆ«â» Î¸, â„“ Î¸ y âˆ‚((Pâ€ Ï€) x)`:
  `âˆ«â» x, â¨… y : ğ“¨, âˆ«â» Î¸, â„“ Î¸ y âˆ‚((Pâ€ Ï€) x) âˆ‚(P âˆ˜â‚˜ Ï€) â‰¤ bayesRisk â„“ P Ï€`
* `IsGenBayesEstimator.isBayesEstimator`: a generalized Bayes estimator is a Bayes estimator.
  That is, it minimizes the Bayesian risk.
* `bayesRisk_eq_of_hasGenBayesEstimator`: if the estimation problem admits a generalized Bayes
estimator, then the Bayesian risk attains the risk lower bound
`âˆ«â» x, â¨… y, âˆ«â» Î¸, â„“ Î¸ y âˆ‚(Pâ€ Ï€) x âˆ‚(P âˆ˜â‚˜ Ï€)`.

## Implementation details


-/

@[expose] public section

open MeasureTheory
open scoped ENNReal NNReal

namespace ProbabilityTheory

variable {Î˜ ğ“§ ğ“¨ : Type*} {mÎ˜ : MeasurableSpace Î˜} {mğ“§ : MeasurableSpace ğ“§} {mğ“¨ : MeasurableSpace ğ“¨}
  {â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ} {P : Kernel Î˜ ğ“§} {Îº : Kernel ğ“§ ğ“¨} {Ï€ : Measure Î˜}

section Posterior

variable [StandardBorelSpace Î˜] [Nonempty Î˜]

/-- The Bayesian risk of an estimator `Îº` with respect to a prior `Ï€` can be expressed as
an integral in the following way: `R_Ï€(Îº) = ((Pâ€ Ï€ Ã— Îº) âˆ˜ P âˆ˜ Ï€)[(Î¸, z) â†¦ â„“(y(Î¸), z)]`. -/
lemma avgRisk_eq_lintegral_posterior_prod
    (hl : Measurable (Function.uncurry â„“)) (P : Kernel Î˜ ğ“§) [IsFiniteKernel P] (Îº : Kernel ğ“§ ğ“¨)
    (Ï€ : Measure Î˜) [IsFiniteMeasure Ï€] [IsSFiniteKernel Îº] :
    avgRisk â„“ P Îº Ï€ = âˆ«â» Î¸y, â„“ Î¸y.1 Î¸y.2 âˆ‚(((Pâ€ Ï€) Ã—â‚– Îº) âˆ˜â‚˜ (P âˆ˜â‚˜ Ï€)) := by
  simp only [avgRisk]
  rw [â† Measure.lintegral_compProd (f := fun Î¸y â†¦ â„“ Î¸y.1 Î¸y.2) (by fun_prop)]
  congr
  calc Ï€ âŠ—â‚˜ (Îº âˆ˜â‚– P) = (Kernel.id âˆ¥â‚– Îº) âˆ˜â‚˜ (Ï€ âŠ—â‚˜ P) := Measure.parallelComp_comp_compProd.symm
  _ = (Kernel.id âˆ¥â‚– Îº) âˆ˜â‚˜ ((Pâ€ Ï€) Ã—â‚– Kernel.id) âˆ˜â‚˜ P âˆ˜â‚˜ Ï€ := by rw [posterior_prod_id_comp]
  _ = ((Pâ€ Ï€) Ã—â‚– Îº) âˆ˜â‚˜ P âˆ˜â‚˜ Ï€ := by
      rw [Measure.comp_assoc, Kernel.parallelComp_comp_prod, Kernel.id_comp, Kernel.comp_id]

lemma avgRisk_eq_lintegral_lintegral_lintegral
    (hl : Measurable (Function.uncurry â„“)) (P : Kernel Î˜ ğ“§) [IsFiniteKernel P] (Îº : Kernel ğ“§ ğ“¨)
    (Ï€ : Measure Î˜) [IsFiniteMeasure Ï€] [IsSFiniteKernel Îº] :
    avgRisk â„“ P Îº Ï€ = âˆ«â» x, âˆ«â» y, âˆ«â» Î¸, â„“ Î¸ y âˆ‚(Pâ€ Ï€) x âˆ‚Îº x âˆ‚(P âˆ˜â‚˜ Ï€) := by
  rw [avgRisk_eq_lintegral_posterior_prod hl,
    Measure.lintegral_bind ((Pâ€ Ï€) Ã—â‚– Îº).aemeasurable (by fun_prop)]
  congr with x
  rw [Kernel.prod_apply, lintegral_prod_symm' _ (by fun_prop)]

lemma lintegral_iInf_posterior_le_avgRisk
    (hl : Measurable (Function.uncurry â„“)) (P : Kernel Î˜ ğ“§) [IsFiniteKernel P] (Îº : Kernel ğ“§ ğ“¨)
    (Ï€ : Measure Î˜) [IsFiniteMeasure Ï€] [IsMarkovKernel Îº] :
    âˆ«â» x, â¨… y : ğ“¨, âˆ«â» Î¸, â„“ Î¸ y âˆ‚((Pâ€ Ï€) x) âˆ‚(P âˆ˜â‚˜ Ï€) â‰¤ avgRisk â„“ P Îº Ï€ := by
  rw [avgRisk_eq_lintegral_lintegral_lintegral hl]
  gcongr with x
  exact iInf_le_lintegral _

lemma lintegral_iInf_posterior_le_bayesRisk
    (hl : Measurable (Function.uncurry â„“)) (P : Kernel Î˜ ğ“§) [IsFiniteKernel P]
    (Ï€ : Measure Î˜) [IsFiniteMeasure Ï€] :
    âˆ«â» x, â¨… y : ğ“¨, âˆ«â» Î¸, â„“ Î¸ y âˆ‚((Pâ€ Ï€) x) âˆ‚(P âˆ˜â‚˜ Ï€) â‰¤ bayesRisk â„“ P Ï€ :=
  le_iInfâ‚‚ fun Îº _ â†¦ lintegral_iInf_posterior_le_avgRisk hl P Îº Ï€

end Posterior

/-- An estimator is a Bayes estimator for a prior `Ï€` if it attains the Bayes risk for `Ï€`. -/
def IsBayesEstimator (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) (Îº : Kernel ğ“§ ğ“¨) (Ï€ : Measure Î˜) : Prop :=
  avgRisk â„“ P Îº Ï€ = bayesRisk â„“ P Ï€

variable [StandardBorelSpace Î˜] [Nonempty Î˜] {f : ğ“§ â†’ ğ“¨} [IsFiniteKernel P] [IsFiniteMeasure Ï€]

/-- We say that a measurable function `f : ğ“§ â†’ ğ“¨` is a generalized Bayes estimator
with respect to the prior `Ï€` if for `(P âˆ˜â‚˜ Ï€)`-almost every `x` it is of
the form `x â†¦ argmin_y Pâ€ Ï€(x)[Î¸ â†¦ â„“ Î¸ y]`. -/
structure IsGenBayesEstimator {ğ“¨ : Type*} [MeasurableSpace ğ“¨]
    (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) [IsFiniteKernel P] (f : ğ“§ â†’ ğ“¨)
    (Ï€ : Measure Î˜) [IsFiniteMeasure Ï€] : Prop where
  measurable : Measurable f
  property : âˆ€áµ x âˆ‚(P âˆ˜â‚˜ Ï€), âˆ«â» Î¸, â„“ Î¸ (f x) âˆ‚(Pâ€ Ï€) x = â¨… y, âˆ«â» Î¸, â„“ Î¸ y âˆ‚(Pâ€ Ï€) x

/-- Given a generalized Bayes estimator `f`, we can define a deterministic kernel. -/
noncomputable
abbrev IsGenBayesEstimator.kernel (h : IsGenBayesEstimator â„“ P f Ï€) : Kernel ğ“§ ğ“¨ :=
  Kernel.deterministic f h.measurable

/-- The risk of a generalized Bayes estimator is the risk lower bound
`âˆ«â» x, â¨… z, âˆ«â» Î¸, â„“ Î¸ z âˆ‚(Pâ€ Ï€) x âˆ‚(P âˆ˜â‚˜ Ï€)`. -/
lemma IsGenBayesEstimator.avgRisk_eq_lintegral_iInf (hf : IsGenBayesEstimator â„“ P f Ï€)
    (hl : Measurable (Function.uncurry â„“)) :
    avgRisk â„“ P hf.kernel Ï€ = âˆ«â» x, â¨… y, âˆ«â» Î¸, â„“ Î¸ y âˆ‚(Pâ€ Ï€) x âˆ‚(P âˆ˜â‚˜ Ï€) := by
  rw [avgRisk_eq_lintegral_lintegral_lintegral hl]
  refine lintegral_congr_ae ?_
  filter_upwards [hf.property] with x hx
  rwa [Kernel.deterministic_apply,
    lintegral_dirac' _ (Measurable.lintegral_prod_left (by fun_prop))]

/-- A generalized Bayes estimator is a Bayes estimator: that is, it minimizes the Bayesian risk. -/
lemma IsGenBayesEstimator.isBayesEstimator (hf : IsGenBayesEstimator â„“ P f Ï€)
    (hl : Measurable (Function.uncurry â„“)) :
    IsBayesEstimator â„“ P hf.kernel Ï€ := by
  simp_rw [IsBayesEstimator]
  apply le_antisymm
  Â· rw [hf.avgRisk_eq_lintegral_iInf hl]
    exact lintegral_iInf_posterior_le_bayesRisk hl _ _
  Â· exact bayesRisk_le_avgRisk _ _ _ _

/-- The estimation problem admits a generalized Bayes estimator with respect to the prior `Ï€`. -/
class HasGenBayesEstimator {ğ“¨ : Type*} [MeasurableSpace ğ“¨]
    (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) [IsFiniteKernel P] (Ï€ : Measure Î˜) [IsFiniteMeasure Ï€] :
    Prop where
  exists_isGenBayesEstimator : âˆƒ f : ğ“§ â†’ ğ“¨, IsGenBayesEstimator â„“ P f Ï€

noncomputable
def HasGenBayesEstimator.estimator (h : HasGenBayesEstimator â„“ P Ï€) : ğ“§ â†’ ğ“¨ :=
  h.exists_isGenBayesEstimator.choose

lemma HasGenBayesEstimator.isGenBayesEstimator (h : HasGenBayesEstimator â„“ P Ï€) :
    IsGenBayesEstimator â„“ P h.estimator Ï€ :=
  h.exists_isGenBayesEstimator.choose_spec

/-- If the estimation problem admits a generalized Bayes estimator, then the Bayesian risk
attains the risk lower bound `âˆ«â» x, â¨… y, âˆ«â» Î¸, â„“ Î¸ y âˆ‚((Pâ€ Ï€) x) âˆ‚(P âˆ˜â‚˜ Ï€)`. -/
lemma bayesRisk_eq_of_hasGenBayesEstimator
    (hl : Measurable (Function.uncurry â„“)) [h : HasGenBayesEstimator â„“ P Ï€] :
    bayesRisk â„“ P Ï€ = âˆ«â» x, â¨… y, âˆ«â» Î¸, â„“ Î¸ y âˆ‚((Pâ€ Ï€) x) âˆ‚(P âˆ˜â‚˜ Ï€) := by
  rw [â† h.isGenBayesEstimator.isBayesEstimator hl,
    h.isGenBayesEstimator.avgRisk_eq_lintegral_iInf hl]

/-- If the set of labels `ğ“¨` is finite, the estimation problem admits a
generalized Bayes estimator. -/
lemma hasGenBayesEstimator_of_finite [Nonempty ğ“¨] [Finite ğ“¨] [MeasurableSingletonClass ğ“¨]
    (hl : Measurable (Function.uncurry â„“)) :
    HasGenBayesEstimator â„“ P Ï€ where
  exists_isGenBayesEstimator := by
    classical
    have : Encodable ğ“¨ := Encodable.ofCountable ğ“¨
    have h_meas y : Measurable (fun x â†¦ âˆ«â» Î¸, â„“ Î¸ y âˆ‚(Pâ€ Ï€) x) :=
      (Measure.measurable_lintegral (by fun_prop)).comp (by fun_prop)
    refine âŸ¨measurableArgmin (fun x y â†¦ âˆ«â» Î¸, â„“ Î¸ y âˆ‚(Pâ€ Ï€) x),
      measurable_measurableArgmin h_meas, ae_of_all _ fun x â†¦ ?_âŸ©
    have h := isMinOn_measurableArgmin (fun x y â†¦ âˆ«â» Î¸, â„“ Î¸ y âˆ‚(Pâ€ Ï€) x) x
    exact le_antisymm (by simpa using h) (iInf_le _ _)

end ProbabilityTheory
