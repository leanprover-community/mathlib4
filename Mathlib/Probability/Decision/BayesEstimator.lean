/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Lorenzo Luccioli
-/

import Mathlib.Probability.Decision.Risk

/-!
# Bayes estimator and generalized Bayes estimator

## Main definitions

* `IsBayesEstimator`: an estimator is a Bayes estimator if it attains the Bayes risk for the prior.
* `IsGenBayesEstimator`: a measurable function `f : ğ“§ â†’ ğ“¨` is a generalized Bayes estimator
  with respect to the prior `Ï€` if for `(P âˆ˜â‚˜ Ï€)`-almost every `x` it has
  the form `x â†¦ argmin_y Pâ€ Ï€(x)[Î¸ â†¦ â„“ Î¸ y]`.
* `HasGenBayesEstimator`: class that states that estimation problem admits a generalized Bayes
  estimator with respect to the prior.

## Main statements

* `IsGenBayesEstimator.isBayesEstimator`: a generalized Bayes estimator is a Bayes estimator.
  That is, it minimizes the Bayesian risk.
* `bayesRiskPrior_eq_of_hasGenBayesEstimator`: if the estimation problem admits a generalized Bayes
estimator, then the Bayesian risk attains the risk lower bound
`âˆ«â» x, â¨… y, âˆ«â» Î¸, â„“ Î¸ y âˆ‚(Pâ€ Ï€) x âˆ‚(P âˆ˜â‚˜ Ï€)`.

## Implementation details


-/

open MeasureTheory
open scoped ENNReal NNReal

namespace MeasurableEmbedding
-- PRed by GaÃ«tan

open Set
variable {Î± Î² : Type*} {mÎ± : MeasurableSpace Î±} {mÎ² : MeasurableSpace Î²} {f : Î± â†’ Î²}

lemma equivRange_apply (hf : MeasurableEmbedding f) (x : Î±) :
    hf.equivRange x = âŸ¨f x, mem_range_self xâŸ© := by
  suffices f x = (hf.equivRange x).1 by simp [this]
  simp [MeasurableEmbedding.equivRange, MeasurableEquiv.cast, MeasurableEquiv.Set.univ,
    MeasurableEmbedding.equivImage]

lemma equivRange_symm_apply_mk (hf : MeasurableEmbedding f) (x : Î±) :
    hf.equivRange.symm âŸ¨f x, mem_range_self xâŸ© = x := by
  have : x = hf.equivRange.symm (hf.equivRange x) := EquivLike.inv_apply_eq.mp rfl
  conv_rhs => rw [this, hf.equivRange_apply]

/-- The left-inverse of a MeasurableEmbedding -/
protected noncomputable
def invFun [Nonempty Î±] (hf : MeasurableEmbedding f) [âˆ€ x, Decidable (x âˆˆ range f)] (x : Î²) : Î± :=
  if hx : x âˆˆ range f then hf.equivRange.symm âŸ¨x, hxâŸ© else (Nonempty.some inferInstance)

@[fun_prop]
lemma measurable_invFun [Nonempty Î±] [âˆ€ x, Decidable (x âˆˆ range f)]
    (hf : MeasurableEmbedding f) :
    Measurable (hf.invFun : Î² â†’ Î±) :=
  Measurable.dite (by fun_prop) measurable_const hf.measurableSet_range

lemma leftInverse_invFun [Nonempty Î±] [âˆ€ x, Decidable (x âˆˆ range f)]
    (hf : MeasurableEmbedding f) :
    Function.LeftInverse hf.invFun f := by
  intro x
  simp only [MeasurableEmbedding.invFun, mem_range, exists_apply_eq_apply, â†“reduceDIte]
  exact hf.equivRange_symm_apply_mk x

end MeasurableEmbedding

lemma measurable_encode {Î± : Type*} {_ : MeasurableSpace Î±} [Encodable Î±]
    [MeasurableSingletonClass Î±] :
    Measurable (Encodable.encode (Î± := Î±)) := by
  refine measurable_to_nat fun a â†¦ ?_
  have : Encodable.encode â»Â¹' {Encodable.encode a} = {a} := by ext; simp
  rw [this]
  exact measurableSet_singleton _

lemma measurableEmbedding_encode (Î± : Type*) {_ : MeasurableSpace Î±} [Encodable Î±]
    [MeasurableSingletonClass Î±] :
    MeasurableEmbedding (Encodable.encode (Î± := Î±)) where
  injective := Encodable.encode_injective
  measurable := measurable_encode
  measurableSet_image' _ _ := .of_discrete

namespace ProbabilityTheory

variable {Î˜ ğ“§ ğ“¨ : Type*} {mÎ˜ : MeasurableSpace Î˜} {mğ“§ : MeasurableSpace ğ“§} {mğ“¨ : MeasurableSpace ğ“¨}
  {â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ} {P : Kernel Î˜ ğ“§} {Îº : Kernel ğ“§ ğ“¨} {Ï€ : Measure Î˜}

/-- An estimator is a Bayes estimator for a prior `Ï€` if it attains the Bayes risk for `Ï€`. -/
def IsBayesEstimator (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) (Îº : Kernel ğ“§ ğ“¨) (Ï€ : Measure Î˜) : Prop :=
  bayesianRisk â„“ P Îº Ï€ = bayesRiskPrior â„“ P Ï€

variable [StandardBorelSpace Î˜] [Nonempty Î˜] {f : ğ“§ â†’ ğ“¨} [IsFiniteKernel P] [IsFiniteMeasure Ï€]

/-- We say that a measurable function `f : ğ“§ â†’ ğ“¨` is a generalized Bayes estimator
with respect to the prior `Ï€` if for `(P âˆ˜â‚˜ Ï€)`-almost every `x` it is of
the form `x â†¦ argmin_y Pâ€ Ï€(x)[Î¸ â†¦ â„“ Î¸ y]`. -/
structure IsGenBayesEstimator {ğ“¨ : Type*} [MeasurableSpace ğ“¨]
    (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) [IsFiniteKernel P] (f : ğ“§ â†’ ğ“¨)
    (Ï€ : Measure Î˜) [IsFiniteMeasure Ï€] : Prop where
  measurable : Measurable f
  property : âˆ€áµ x âˆ‚(P âˆ˜â‚˜ Ï€), âˆ«â» Î¸, â„“ Î¸ (f x) âˆ‚(Pâ€ Ï€) x = â¨… y, âˆ«â» Î¸, â„“ Î¸ y âˆ‚(Pâ€ Ï€) x

/-- Given a generalized Bayes estimator `f`, we can define a deterministic kernel. -/
noncomputable
abbrev IsGenBayesEstimator.kernel (h : IsGenBayesEstimator â„“ P f Ï€) : Kernel ğ“§ ğ“¨ :=
  Kernel.deterministic f h.measurable

/-- The risk of a generalized Bayes estimator is the risk lower bound
`âˆ«â» x, â¨… z, âˆ«â» Î¸, â„“ Î¸ z âˆ‚(Pâ€ Ï€) x âˆ‚(P âˆ˜â‚˜ Ï€)`. -/
lemma IsGenBayesEstimator.bayesianRisk_eq_lintegral_iInf (hf : IsGenBayesEstimator â„“ P f Ï€)
    (hl : Measurable (Function.uncurry â„“)) :
    bayesianRisk â„“ P hf.kernel Ï€ = âˆ«â» x, â¨… y, âˆ«â» Î¸, â„“ Î¸ y âˆ‚(Pâ€ Ï€) x âˆ‚(P âˆ˜â‚˜ Ï€) := by
  rw [bayesianRisk_eq_lintegral_lintegral_lintegral hl]
  refine lintegral_congr_ae ?_
  filter_upwards [hf.property] with x hx
  rwa [Kernel.deterministic_apply,
    lintegral_dirac' _ (Measurable.lintegral_prod_left (by fun_prop))]

/-- A generalized Bayes estimator is a Bayes estimator: that is, it minimizes the Bayesian risk. -/
lemma IsGenBayesEstimator.isBayesEstimator (hf : IsGenBayesEstimator â„“ P f Ï€)
    (hl : Measurable (Function.uncurry â„“)) :
    IsBayesEstimator â„“ P hf.kernel Ï€ := by
  simp_rw [IsBayesEstimator]
  apply le_antisymm
  Â· rw [hf.bayesianRisk_eq_lintegral_iInf hl]
    exact lintegral_iInf_posterior_le_bayesRiskPrior hl _ _
  Â· exact bayesRiskPrior_le_bayesianRisk _ _ _ _

/-- The estimation problem admits a generalized Bayes estimator with respect to the prior `Ï€`. -/
class HasGenBayesEstimator {ğ“¨ : Type*} [MeasurableSpace ğ“¨]
    (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) [IsFiniteKernel P] (Ï€ : Measure Î˜) [IsFiniteMeasure Ï€] :
    Prop where
  exists_isGenBayesEstimator : âˆƒ f : ğ“§ â†’ ğ“¨, IsGenBayesEstimator â„“ P f Ï€

noncomputable
def HasGenBayesEstimator.estimator (h : HasGenBayesEstimator â„“ P Ï€) : ğ“§ â†’ ğ“¨ :=
  h.exists_isGenBayesEstimator.choose

lemma HasGenBayesEstimator.isGenBayesEstimator (h : HasGenBayesEstimator â„“ P Ï€) :
    IsGenBayesEstimator â„“ P h.estimator Ï€ :=
  h.exists_isGenBayesEstimator.choose_spec

/-- If the estimation problem admits a generalized Bayes estimator, then the Bayesian risk
attains the risk lower bound `âˆ«â» x, â¨… y, âˆ«â» Î¸, â„“ Î¸ y âˆ‚((Pâ€ Ï€) x) âˆ‚(P âˆ˜â‚˜ Ï€)`. -/
lemma bayesRiskPrior_eq_of_hasGenBayesEstimator
    (hl : Measurable (Function.uncurry â„“)) [h : HasGenBayesEstimator â„“ P Ï€] :
    bayesRiskPrior â„“ P Ï€ = âˆ«â» x, â¨… y, âˆ«â» Î¸, â„“ Î¸ y âˆ‚((Pâ€ Ï€) x) âˆ‚(P âˆ˜â‚˜ Ï€) := by
  rw [â† h.isGenBayesEstimator.isBayesEstimator hl,
    h.isGenBayesEstimator.bayesianRisk_eq_lintegral_iInf hl]

section Finite

variable {Î± : Type*} {_ : MeasurableSpace Î±} [TopologicalSpace Î±] [LinearOrder Î±]
    [OpensMeasurableSpace Î±] [OrderClosedTopology Î±] [SecondCountableTopology Î±]

lemma measurableSet_isMin [Countable ğ“¨]
    {f : ğ“§ â†’ ğ“¨ â†’ Î±} (hf : âˆ€ y, Measurable (fun x â†¦ f x y)) (y : ğ“¨) :
    MeasurableSet {x | âˆ€ z, f x y â‰¤ f x z} := by
  rw [show {x | âˆ€ y', f x y â‰¤ f x y'} = â‹‚ y', {x | f x y â‰¤ f x y'} by ext; simp]
  exact MeasurableSet.iInter fun z â†¦ measurableSet_le (by fun_prop) (by fun_prop)

lemma exists_isMinOn' {Î± : Type*} [LinearOrder Î±]
    [Nonempty ğ“¨] [Finite ğ“¨] [Encodable ğ“¨] (f : ğ“§ â†’ ğ“¨ â†’ Î±) (x : ğ“§) :
    âˆƒ n : â„•, âˆƒ y, n = Encodable.encode y âˆ§ âˆ€ z, f x y â‰¤ f x z := by
  obtain âŸ¨y, hâŸ© := Finite.exists_min (f x)
  exact âŸ¨Encodable.encode y, y, rfl, hâŸ©

noncomputable
def measurableArgmin [Nonempty ğ“¨] [Finite ğ“¨] [Encodable ğ“¨] [MeasurableSingletonClass ğ“¨]
    [(x : â„•) â†’ Decidable (x âˆˆ Set.range (Encodable.encode (Î± := ğ“¨)))]
    (f : ğ“§ â†’ ğ“¨ â†’ Î±)
    [âˆ€ x, DecidablePred fun n â†¦ âˆƒ y, n = Encodable.encode y âˆ§ âˆ€ (z : ğ“¨), f x y â‰¤ f x z]
    (x : ğ“§) :
    ğ“¨ :=
  (measurableEmbedding_encode ğ“¨).invFun (Nat.find (exists_isMinOn' f x))

lemma measurable_measurableArgmin [Nonempty ğ“¨] [Finite ğ“¨] [Encodable ğ“¨] [MeasurableSingletonClass ğ“¨]
    [(x : â„•) â†’ Decidable (x âˆˆ Set.range (Encodable.encode (Î± := ğ“¨)))]
    {f : ğ“§ â†’ ğ“¨ â†’ Î±}
    [âˆ€ x, DecidablePred fun n â†¦ âˆƒ y, n = Encodable.encode y âˆ§ âˆ€ (z : ğ“¨), f x y â‰¤ f x z]
    (hf : âˆ€ y, Measurable (fun x â†¦ f x y)) :
    Measurable (measurableArgmin f) := by
  refine (MeasurableEmbedding.measurable_invFun (measurableEmbedding_encode ğ“¨)).comp ?_
  refine measurable_find _ fun n â†¦ ?_
  have : {x | âˆƒ y, n = Encodable.encode y âˆ§ âˆ€ (z : ğ“¨), f x y â‰¤ f x z}
      = â‹ƒ y, ({x | n = Encodable.encode y} âˆ© {x | âˆ€ z, f x y â‰¤ f x z}) := by ext; simp
  rw [this]
  refine MeasurableSet.iUnion fun y â†¦ (MeasurableSet.inter (by simp) ?_)
  exact measurableSet_isMin (by fun_prop) y

lemma isMinOn_measurableArgmin {Î± : Type*} [LinearOrder Î±]
    [Nonempty ğ“¨] [Finite ğ“¨] [Encodable ğ“¨] [MeasurableSingletonClass ğ“¨]
    [(x : â„•) â†’ Decidable (x âˆˆ Set.range (Encodable.encode (Î± := ğ“¨)))]
    (f : ğ“§ â†’ ğ“¨ â†’ Î±)
    [âˆ€ x, DecidablePred fun n â†¦ âˆƒ y, n = Encodable.encode y âˆ§ âˆ€ (z : ğ“¨), f x y â‰¤ f x z]
    (x : ğ“§) (z : ğ“¨) :
    f x (measurableArgmin f x) â‰¤ f x z := by
  obtain âŸ¨y, h_eq, h_leâŸ© := Nat.find_spec (exists_isMinOn' f x)
  refine le_trans (le_of_eq ?_) (h_le z)
  rw [measurableArgmin, h_eq,
    MeasurableEmbedding.leftInverse_invFun (measurableEmbedding_encode ğ“¨) y]

lemma hasGenBayesEstimator_of_finite [Nonempty ğ“¨] [Finite ğ“¨] [MeasurableSingletonClass ğ“¨]
    (hl : Measurable (Function.uncurry â„“)) :
    HasGenBayesEstimator â„“ P Ï€ where
  exists_isGenBayesEstimator := by
    classical
    have : Encodable ğ“¨ := Encodable.ofCountable ğ“¨
    have h_meas y : Measurable (fun x â†¦ âˆ«â» Î¸, â„“ Î¸ y âˆ‚(Pâ€ Ï€) x) :=
      (Measure.measurable_lintegral (by fun_prop)).comp (by fun_prop)
    refine âŸ¨measurableArgmin (fun x y â†¦ âˆ«â» Î¸, â„“ Î¸ y âˆ‚(Pâ€ Ï€) x),
      measurable_measurableArgmin h_meas, ae_of_all _ fun x â†¦ ?_âŸ©
    have h := isMinOn_measurableArgmin (fun x y â†¦ âˆ«â» Î¸, â„“ Î¸ y âˆ‚(Pâ€ Ï€) x) x
    exact le_antisymm (by simpa using h) (iInf_le _ _)

end Finite

end ProbabilityTheory
