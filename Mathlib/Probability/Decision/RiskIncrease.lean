/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Lorenzo Luccioli
-/
import Mathlib.Probability.Decision.Binary

/-!
# Statistical information

TODO: cite DeGroot and also Duchi et al.

## Main definitions

* `statInfo`

## Main statements

* `statInfo_comp_le`: data-processing inequality

## Notation

## Implementation details

-/

open MeasureTheory Set Function

open scoped ENNReal NNReal

namespace ProbabilityTheory

lemma Kernel.withDensity_comp {Î± Î² Î³ : Type*} {_ : MeasurableSpace Î±} {_ : MeasurableSpace Î²}
    {_ : MeasurableSpace Î³} {Îº : Kernel Î± Î²} [IsSFiniteKernel Îº]
    {Î· : Kernel Î² Î³} [IsSFiniteKernel Î·] {f : Î± â†’ â„â‰¥0âˆ} (hf : Measurable f) :
    (Î· âˆ˜â‚– Îº).withDensity (fun a _ â†¦ f a) = Î· âˆ˜â‚– (Îº.withDensity (fun a _ â†¦ f a)) := by
  ext a s hs
  rw [Kernel.withDensity_apply _ (by fun_prop), Kernel.comp_apply, Kernel.comp_apply]
  simp only [withDensity_const, Measure.smul_apply, smul_eq_mul]
  conv_rhs => rw [Measure.bind_apply hs (by fun_prop)]
  rw [lintegral_withDensity _ (by fun_prop)]
  swap; Â· exact Î·.measurable_coe hs
  rw [Measure.bind_apply hs (Kernel.aemeasurable _), lintegral_const_mul]
  exact Î·.measurable_coe hs

variable {Î˜ ğ“§ ğ“§' ğ“¨ : Type*} {mÎ˜ : MeasurableSpace Î˜} {mğ“§ : MeasurableSpace ğ“§}
  {mğ“§' : MeasurableSpace ğ“§'} [MeasurableSpace ğ“¨]
  {Ï€ : Measure Î˜} {P : Kernel Î˜ ğ“§} {â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ}

noncomputable
def riskIncrease (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) (Ï€ : Measure Î˜) : â„â‰¥0âˆ :=
  bayesRiskPrior â„“ (Kernel.discard ğ“§ âˆ˜â‚– P) Ï€ - bayesRiskPrior â„“ P Ï€

lemma riskIncrease_eq_iInf_sub' [Nonempty ğ“¨] (hl : Measurable (uncurry â„“)) (P : Kernel Î˜ ğ“§)
    (Ï€ : Measure Î˜) [SFinite Ï€] :
    riskIncrease â„“ P Ï€ = (â¨… z : ğ“¨, âˆ«â» Î¸, P Î¸ univ * â„“ Î¸ z âˆ‚Ï€) - bayesRiskPrior â„“ P Ï€ := by
  simp_rw [riskIncrease]
  simp [bayesRiskPrior_of_subsingleton hl, mul_comm]

lemma riskIncrease_eq_iInf_sub (hl : Measurable (uncurry â„“)) (P : Kernel Î˜ ğ“§) [IsMarkovKernel P]
    (Ï€ : Measure Î˜) [SFinite Ï€] :
    riskIncrease â„“ P Ï€ = (â¨… z : ğ“¨, âˆ«â» Î¸, â„“ Î¸ z âˆ‚Ï€) - bayesRiskPrior â„“ P Ï€ := by
  simp_rw [riskIncrease, Kernel.comp_discard, bayesRiskPrior_discard hl]

@[simp] lemma riskIncrease_of_isEmpty_of_isEmpty [IsEmpty ğ“§] [IsEmpty ğ“¨] :
    riskIncrease â„“ P Ï€ = âˆ := by
  simp [riskIncrease]

@[simp] lemma riskIncrease_of_nonempty_of_isEmpty [Nonempty ğ“§] [IsEmpty ğ“¨] :
    riskIncrease â„“ P Ï€ = 0 := by
  simp [riskIncrease]

@[simp] lemma riskIncrease_zero_left [Nonempty ğ“¨] : riskIncrease â„“ (0 : Kernel Î˜ ğ“§) Ï€ = 0 := by
  simp [riskIncrease]

@[simp] lemma riskIncrease_zero_right [Nonempty ğ“¨] : riskIncrease â„“ P (0 : Measure Î˜) = 0 := by
  simp [riskIncrease]

@[simp] lemma riskIncrease_const (hl : Measurable (uncurry â„“))
    [SFinite Ï€] {Î¼ : Measure ğ“§} [IsProbabilityMeasure Î¼] :
    riskIncrease â„“ (Kernel.const Î˜ Î¼) Ï€ = 0 := by
  simp [riskIncrease, bayesRiskPrior_const hl]

lemma riskIncrease_le_iInf' [Nonempty ğ“¨] (hl : Measurable (uncurry â„“)) [SFinite Ï€] :
    riskIncrease â„“ P Ï€ â‰¤ â¨… z : ğ“¨, âˆ«â» Î¸, P Î¸ univ * â„“ Î¸ z âˆ‚Ï€ :=
  riskIncrease_eq_iInf_sub' hl P Ï€ â–¸ tsub_le_self

lemma riskIncrease_le_iInf (hl : Measurable (uncurry â„“)) [IsMarkovKernel P] [SFinite Ï€] :
    riskIncrease â„“ P Ï€ â‰¤ â¨… z : ğ“¨, âˆ«â» Î¸, â„“ Î¸ z âˆ‚Ï€ :=
  riskIncrease_eq_iInf_sub hl P Ï€ â–¸ tsub_le_self

lemma riskIncrease_lt_top' [Nonempty ğ“¨] (hl : Measurable (uncurry â„“))
    [IsFiniteMeasure Ï€] {y : ğ“¨} (h_finite : âˆ«â» Î¸, P Î¸ univ * â„“ Î¸ y âˆ‚Ï€ â‰  âŠ¤) :
    riskIncrease â„“ P Ï€ < âŠ¤ :=
  (riskIncrease_le_iInf' hl).trans_lt (iInf_lt_top.mpr âŸ¨y, h_finite.lt_topâŸ©)

lemma riskIncrease_lt_top (hl : Measurable (uncurry â„“)) [IsMarkovKernel P] [IsFiniteMeasure Ï€]
    {y : ğ“¨} (h_finite : âˆ«â» Î¸, â„“ Î¸ y âˆ‚Ï€ â‰  âŠ¤) :
    riskIncrease â„“ P Ï€ < âŠ¤ :=
  (riskIncrease_le_iInf hl).trans_lt (iInf_lt_top.mpr âŸ¨y, h_finite.lt_topâŸ©)

/-- **Data processing inequality** for the risk increase. -/
lemma riskIncrease_comp_le (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) (Ï€ : Measure Î˜)
    (Î· : Kernel ğ“§ ğ“§') [IsMarkovKernel Î·] :
    riskIncrease â„“ (Î· âˆ˜â‚– P) Ï€ â‰¤ riskIncrease â„“ P Ï€ := by
  refine tsub_le_tsub ?_ (bayesRiskPrior_le_bayesRiskPrior_comp _ _ _ _)
  rw [â† Kernel.comp_assoc]
  simp

lemma riskIncrease_withDensity (hl : Measurable (Function.uncurry â„“))
    (P : Kernel Î˜ ğ“§) [IsSFiniteKernel P] (Ï€ : Measure Î˜) [SFinite Ï€]
    {f : Î˜ â†’ â„â‰¥0âˆ} (hf : Measurable f) :
    riskIncrease â„“ (P.withDensity (fun Î¸ _ â†¦ f Î¸)) Ï€ = riskIncrease â„“ P (Ï€.withDensity f) := by
  rw [riskIncrease, â† Kernel.withDensity_comp hf, bayesRiskPrior_withDensity hl _ Ï€ hf,
    bayesRiskPrior_withDensity hl _ Ï€ hf, riskIncrease]

end ProbabilityTheory
