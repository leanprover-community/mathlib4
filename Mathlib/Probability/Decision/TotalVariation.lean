/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Probability.Decision.StatInfo

/-!
# Total variation distance

## Main definitions

* `FooBar`

## Main statements

* `fooBar_unique`

## Notation



## Implementation details



## References

* [F. Bar, *Quuxes*][bibkey]

## Tags

Foobars, barfoos
-/

open MeasureTheory Bool

open scoped ENNReal

namespace ProbabilityTheory

variable {ğ“§ ğ“¨ : Type*} {mğ“§ : MeasurableSpace ğ“§} {mğ“¨ : MeasurableSpace ğ“¨}
  {Î¼ Î½ : Measure ğ“§}

/-- Total variation distance between two measures. -/
noncomputable def tv (Î¼ Î½ : Measure ğ“§) : â„ := (statInfo Î¼ Î½ (boolMeasure 1 1)).toReal

instance : IsFiniteMeasure (boolMeasure 1 1) := by constructor; simp

@[simp] lemma tv_zero_left : tv (0 : Measure ğ“§) Î½ = 0 := by simp [tv]

@[simp] lemma tv_zero_right : tv Î¼ (0 : Measure ğ“§) = 0 := by simp [tv]

@[simp] lemma tv_self : tv Î¼ Î¼ = 0 := by simp [tv]

lemma tv_nonneg : 0 â‰¤ tv Î¼ Î½ := ENNReal.toReal_nonneg

lemma tv_le [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½] :
    tv Î¼ Î½ â‰¤ min (Î¼.real .univ) (Î½.real .univ) := by
  rw [Measure.real, Measure.real, â† ENNReal.toReal_min (measure_ne_top _ _) (measure_ne_top _ _)]
  refine ENNReal.toReal_mono ?_ ?_
  Â· simp
  Â· have h := statInfo_le_min (Î¼ := Î¼) (Î½ := Î½) (Ï€ := boolMeasure 1 1)
    simpa only [boolMeasure_apply_false, one_mul, boolMeasure_apply_true] using h

/-- **Data processing inequality** for the total variation. -/
lemma tv_comp_le (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼] (Îº : Kernel ğ“§ ğ“¨) [IsMarkovKernel Îº] :
    tv (Îº âˆ˜â‚˜ Î¼) (Îº âˆ˜â‚˜ Î½) â‰¤ tv Î¼ Î½ :=
  ENNReal.toReal_mono statInfo_ne_top (statInfo_comp_le _ _ _ _)

lemma tv_eq_min_sub_iInf_measurableSet (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½] :
    tv Î¼ Î½ = min (Î¼.real .univ) (Î½.real .univ)
      - â¨… (E : {E // MeasurableSet E}), Î¼.real E + Î½.real E.1á¶œ := by
  have h := statInfo_eq_min_sub_iInf_measurableSet Î¼ Î½ (boolMeasure 1 1)
  simp only [boolMeasure_apply_false, one_mul, boolMeasure_apply_true] at h
  rw [tv, h]
  rw [Measure.real, Measure.real, â† ENNReal.toReal_min (measure_ne_top _ _) (measure_ne_top _ _),
    ENNReal.toReal_sub_of_le]
  Â· congr
    rw [iInf_subtype']
    rw [â† ENNReal.toReal_ofReal (r := â¨… (E : {E //  MeasurableSet E}), Î¼.real E + Î½.real E.1á¶œ)]
    swap; Â· exact le_ciInf fun E â†¦ by positivity
    simp_rw [ENNReal.ofReal_iInf]
    congr with E
    rw [ENNReal.ofReal_add (by positivity) (by positivity)]
    simp
  Â· simp only [le_inf_iff]
    constructor
    Â· exact (iInf_le _ .univ).trans (by simp)
    Â· exact (iInf_le _ âˆ…).trans (by simp)
  Â· simp

lemma tv_eq_one_sub_iInf_measurableSet (Î¼ Î½ : Measure ğ“§) [IsProbabilityMeasure Î¼]
    [IsProbabilityMeasure Î½] :
    tv Î¼ Î½ = 1 - â¨… (E : {E // MeasurableSet E}), Î¼.real E + Î½.real E.1á¶œ := by
  simp [tv_eq_min_sub_iInf_measurableSet]

end ProbabilityTheory
