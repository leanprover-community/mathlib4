/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Lorenzo Luccioli
-/

import Mathlib.Probability.Kernel.Posterior

/-!
# Risk of an estimator

An estimation problem is defined by a parameter space `Î˜`, a data generating kernel `P : Kernel Î˜ ğ“§`
and a loss function `â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ`.
A (randomized) estimator is a kernel `Îº : Kernel ğ“§ ğ“¨` that maps data to estimates of a quantity
of interest that depends on the parameter. Often the quantity of interest is the parameter itself
and `ğ“¨ = Î˜`.
The quality of an estimate `y` when data comes from the distribution with parameter `Î¸` is measured
by the loss function `â„“ Î¸ z`.

## Main definitions

* `risk â„“ P Îº Î¸`
* `bayesianRisk â„“ P Îº Ï€`
* `bayesRiskPrior â„“ P Ï€`
* `bayesRisk â„“ P`

## Main statements

* `fooBar_unique`

## Implementation details


-/

open MeasureTheory
open scoped ENNReal NNReal

namespace ProbabilityTheory

variable {Î˜ Î˜' ğ“§ ğ“§' ğ“¨ : Type*} {mÎ˜ : MeasurableSpace Î˜} {mÎ˜' : MeasurableSpace Î˜'}
  {mğ“§ : MeasurableSpace ğ“§} {mğ“§' : MeasurableSpace ğ“§'} {mğ“¨ : MeasurableSpace ğ“¨}
  {â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ} {P : Kernel Î˜ ğ“§} {Îº : Kernel ğ“§ ğ“¨} {Ï€ : Measure Î˜}

section Definitions

/-- The risk of an estimator `Îº` on an estimation task with loss `â„“` and data generating kernel `P`
at the parameter `Î¸`. -/
noncomputable
def risk (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) (Îº : Kernel ğ“§ ğ“¨) (Î¸ : Î˜) : â„â‰¥0âˆ :=
  âˆ«â» z, â„“ Î¸ z âˆ‚((Îº âˆ˜â‚– P) Î¸)

/-- The bayesian risk of an estimator `Îº` on an estimation task with loss `â„“` and
data generating kernel `P` with respect to a prior `Ï€`. -/
noncomputable
def bayesianRisk (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) (Îº : Kernel ğ“§ ğ“¨) (Ï€ : Measure Î˜) : â„â‰¥0âˆ :=
  âˆ«â» Î¸, risk â„“ P Îº Î¸ âˆ‚Ï€

/-- The Bayes risk with respect to a prior `Ï€`, defined as the infimum of the Bayesian risks of all
estimators. -/
noncomputable
def bayesRiskPrior {ğ“¨ : Type*} [MeasurableSpace ğ“¨]
    (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) (Ï€ : Measure Î˜) : â„â‰¥0âˆ :=
  â¨… (Îº : Kernel ğ“§ ğ“¨) (_ : IsMarkovKernel Îº), bayesianRisk â„“ P Îº Ï€

/-- The Bayes risk, defined as the supremum over priors of the Bayes risk with respect to
the prior. -/
noncomputable
def bayesRisk {ğ“¨ : Type*} [MeasurableSpace ğ“¨] (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) : â„â‰¥0âˆ :=
  â¨† (Ï€ : Measure Î˜) (_ : IsProbabilityMeasure Ï€), bayesRiskPrior â„“ P Ï€

/-- The minimax risk, defined as the infimum over estimators of the maximal risk of
the estimator. -/
noncomputable
def minimaxRisk {ğ“¨ : Type*} [MeasurableSpace ğ“¨] (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) : â„â‰¥0âˆ :=
  â¨… (Îº : Kernel ğ“§ ğ“¨) (_ : IsMarkovKernel Îº), â¨† Î¸, risk â„“ P Îº Î¸

end Definitions

@[simp]
lemma bayesianRisk_of_isEmpty [IsEmpty Î˜] : bayesianRisk â„“ P Îº Ï€ = 0 := by simp [bayesianRisk]

lemma bayesianRisk_le_iSup_risk (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) (Îº : Kernel ğ“§ ğ“¨)
    (Ï€ : Measure Î˜) [IsProbabilityMeasure Ï€] :
    bayesianRisk â„“ P Îº Ï€ â‰¤ â¨† Î¸, risk â„“ P Îº Î¸ := by
  rw [bayesianRisk]
  calc âˆ«â» Î¸, risk â„“ P Îº Î¸ âˆ‚Ï€
  _ â‰¤ âˆ«â» _, (â¨† Î¸', risk â„“ P Îº Î¸') âˆ‚Ï€ := lintegral_mono (fun Î¸ â†¦ le_iSup _ _)
  _ = â¨† Î¸, risk â„“ P Îº Î¸ := by simp

lemma bayesianRisk_comap_measurableEquiv (hl : Measurable (Function.uncurry â„“)) (P : Kernel Î˜ ğ“§)
    [IsSFiniteKernel P]
    (Îº : Kernel ğ“§ ğ“¨) [IsSFiniteKernel Îº] (Ï€ : Measure Î˜) (e : Î˜ â‰ƒáµ Î˜') :
    bayesianRisk (fun Î¸ z â†¦ â„“ (e.symm Î¸) z) (P.comap e.symm e.symm.measurable)
      Îº (Ï€.map e) = bayesianRisk â„“ P Îº Ï€ := by
  simp only [bayesianRisk, risk]
  rw [lintegral_map _ e.measurable]
  Â· congr with Î¸
    congr
    Â· ext z hz
      simp [Îº.comp_apply' _ _ hz, Kernel.comap_apply]
    Â· simp
  Â· fun_prop

/-- **Data processing inequality** for the Bayes risk with respect to a prior. -/
lemma bayesRiskPrior_le_bayesRiskPrior_comp (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§)
    (Ï€ : Measure Î˜) (Î· : Kernel ğ“§ ğ“§') [IsMarkovKernel Î·] :
    bayesRiskPrior â„“ P Ï€ â‰¤ bayesRiskPrior â„“ (Î· âˆ˜â‚– P) Ï€ := by
  simp only [bayesRiskPrior, bayesianRisk, risk, le_iInf_iff]
  intro Îº hÎº
  rw [â† Îº.comp_assoc Î·]
  exact iInf_le_of_le (Îº âˆ˜â‚– Î·) (iInf_le_of_le inferInstance le_rfl)

/-- An estimator is a Bayes estimator for a prior `Ï€` if it attains the Bayes risk for `Ï€`. -/
def IsBayesEstimator (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) (Îº : Kernel ğ“§ ğ“¨) (Ï€ : Measure Î˜) : Prop :=
  bayesianRisk â„“ P Îº Ï€ = bayesRiskPrior â„“ P Ï€

lemma bayesRiskPrior_le_minimaxRisk (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§)
    (Ï€ : Measure Î˜) [IsProbabilityMeasure Ï€] :
    bayesRiskPrior â„“ P Ï€ â‰¤ minimaxRisk â„“ P :=
  iInf_mono (fun _ â†¦ iInf_mono fun _ â†¦ bayesianRisk_le_iSup_risk _ _ _ _)

lemma bayesRisk_le_minimaxRisk (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) :
    bayesRisk â„“ P â‰¤ minimaxRisk â„“ P := by
  simp only [bayesRisk, iSup_le_iff]
  exact fun _ _ â†¦ bayesRiskPrior_le_minimaxRisk _ _ _

/-! ### Properties of the Bayes risk of a prior -/

lemma bayesRiskPrior_compProd_le_bayesRiskPrior (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§)
    [IsSFiniteKernel P] (Ï€ : Measure Î˜) (Îº : Kernel (Î˜ Ã— ğ“§) ğ“§') [IsMarkovKernel Îº] :
    bayesRiskPrior â„“ (P âŠ—â‚– Îº) Ï€ â‰¤ bayesRiskPrior â„“ P Ï€ := by
  have : P = (Kernel.deterministic Prod.fst (by fun_prop)) âˆ˜â‚– (P âŠ—â‚– Îº) := by
    rw [Kernel.deterministic_comp_eq_map, â† Kernel.fst_eq, Kernel.fst_compProd]
  nth_rw 2 [this]
  exact bayesRiskPrior_le_bayesRiskPrior_comp _ _ _ _

-- Do we also need a version without the `IsMarkovKernel` assumption? it would be of the form:
-- `bayesRiskPrior E Ï€ â‰¤ â¨… z : ğ“¨, âˆ«â» Î¸, E.â„“ (E.y Î¸, z) * (E.P Î¸) .univ âˆ‚Ï€`
lemma bayesRiskPrior_le_inf (hl : Measurable (Function.uncurry â„“)) (P : Kernel Î˜ ğ“§)
    (Ï€ : Measure Î˜) [IsMarkovKernel P] :
    bayesRiskPrior â„“ P Ï€ â‰¤ â¨… z : ğ“¨, âˆ«â» Î¸, â„“ Î¸ z âˆ‚Ï€ := by
  simp_rw [le_iInf_iff, bayesRiskPrior]
  refine fun z â†¦ iInf_le_of_le (Kernel.const _ (Measure.dirac z)) ?_
  convert iInf_le _ ?_ using 1
  Â· simp_rw [bayesianRisk, risk, Kernel.const_comp', Kernel.const_apply]
    congr with Î¸
    rw [lintegral_dirac']
    fun_prop
  Â· infer_instance

/-- The Bayesian risk of an estimator `Îº` with respect to a prior `Ï€` can be expressed as
an integral in the following way: `R_Ï€(Îº) = ((Pâ€ Ï€ Ã— Îº) âˆ˜ P âˆ˜ Ï€)[(Î¸, z) â†¦ â„“(y(Î¸), z)]`. -/
lemma bayesianRisk_eq_lintegral_bayesInv_prod [StandardBorelSpace Î˜] [Nonempty Î˜]
    (hl : Measurable (Function.uncurry â„“)) (P : Kernel Î˜ ğ“§) [IsFiniteKernel P] (Îº : Kernel ğ“§ ğ“¨)
    (Ï€ : Measure Î˜) [IsFiniteMeasure Ï€] [IsSFiniteKernel Îº] :
    bayesianRisk â„“ P Îº Ï€ = âˆ«â» (Î¸z : Î˜ Ã— ğ“¨), â„“ Î¸z.1 Î¸z.2 âˆ‚(((Pâ€ Ï€) Ã—â‚– Îº) âˆ˜â‚˜ (P âˆ˜â‚˜ Ï€)) := by
  simp only [bayesianRisk, risk]
  rw [â† Measure.lintegral_compProd (f := fun Î¸z â†¦ â„“ Î¸z.1 Î¸z.2) (by fun_prop)]
  congr
  calc Ï€ âŠ—â‚˜ (Îº âˆ˜â‚– P) = (Kernel.id âˆ¥â‚– Îº) âˆ˜â‚˜ (Ï€ âŠ—â‚˜ P) := Measure.parallelComp_comp_compProd.symm
  _ = (Kernel.id âˆ¥â‚– Îº) âˆ˜â‚˜ ((Pâ€ Ï€) Ã—â‚– Kernel.id) âˆ˜â‚˜ P âˆ˜â‚˜ Ï€ := by rw [posterior_prod_id_comp]
  _ = ((Pâ€ Ï€) Ã—â‚– Îº) âˆ˜â‚˜ P âˆ˜â‚˜ Ï€ := by
      rw [Measure.comp_assoc, Kernel.parallelComp_comp_prod, Kernel.id_comp, Kernel.comp_id]

lemma bayesianRisk_eq_integral_integral_integral [StandardBorelSpace Î˜] [Nonempty Î˜]
    (hl : Measurable (Function.uncurry â„“)) (P : Kernel Î˜ ğ“§) [IsFiniteKernel P] (Îº : Kernel ğ“§ ğ“¨)
    (Ï€ : Measure Î˜) [IsFiniteMeasure Ï€] [IsSFiniteKernel Îº] :
    bayesianRisk â„“ P Îº Ï€ = âˆ«â» x, âˆ«â» z, âˆ«â» Î¸, â„“ Î¸ z âˆ‚(Pâ€ Ï€) x âˆ‚Îº x âˆ‚(P âˆ˜â‚˜ Ï€) := by
  rw [bayesianRisk_eq_lintegral_bayesInv_prod hl,
    Measure.lintegral_bind ((Pâ€ Ï€) Ã—â‚– Îº).aemeasurable (by fun_prop)]
  congr with x
  rw [Kernel.prod_apply, lintegral_prod_symm' _ (by fun_prop)]

lemma bayesianRisk_ge_lintegral_iInf_bayesInv [StandardBorelSpace Î˜] [Nonempty Î˜]
    (hl : Measurable (Function.uncurry â„“)) (P : Kernel Î˜ ğ“§) [IsFiniteKernel P] (Îº : Kernel ğ“§ ğ“¨)
    (Ï€ : Measure Î˜) [IsFiniteMeasure Ï€] [IsMarkovKernel Îº] :
    bayesianRisk â„“ P Îº Ï€ â‰¥ âˆ«â» x, â¨… z : ğ“¨, âˆ«â» Î¸, â„“ Î¸ z âˆ‚((Pâ€ Ï€) x) âˆ‚(P âˆ˜â‚˜ Ï€) := by
  rw [bayesianRisk_eq_integral_integral_integral hl]
  gcongr with x
  calc
    _ â‰¥ âˆ«â» _, â¨… z, âˆ«â» (Î¸ : Î˜), â„“ Î¸ z âˆ‚(Pâ€ Ï€) x âˆ‚Îº x := lintegral_mono fun z â†¦ iInf_le _ z
    _ = â¨… z, âˆ«â» (Î¸ : Î˜), â„“ Î¸ z âˆ‚(Pâ€ Ï€) x := by rw [lintegral_const, measure_univ, mul_one]

section IsGenBayesEstimator

/-! ### Generalized Bayes estimator -/

variable [StandardBorelSpace Î˜] [Nonempty Î˜] {f : ğ“§ â†’ ğ“¨}
  [IsFiniteKernel P] [IsFiniteMeasure Ï€]

/-- We say that a measurable function `f : ğ“§ â†’ ğ“¨` is a Generalized Bayes estimator for the
estimation problem `E` with respect to the prior `Ï€` if for `(P âˆ˜â‚˜ Ï€)`-almost every `x` it is of
the form `x â†¦ argmin_z Pâ€ Ï€(x)[Î¸ â†¦ â„“(y(Î¸), z)]`. -/
structure IsGenBayesEstimator {ğ“¨ : Type*} [MeasurableSpace ğ“¨]
    (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) [IsFiniteKernel P] (f : ğ“§ â†’ ğ“¨)
    (Ï€ : Measure Î˜) [IsFiniteMeasure Ï€] : Prop where
  measurable : Measurable f
  property : âˆ€áµ x âˆ‚(P âˆ˜â‚˜ Ï€), âˆ«â» Î¸, â„“ Î¸ (f x) âˆ‚(Pâ€ Ï€) x
    = â¨… z, âˆ«â» Î¸, â„“ Î¸ z âˆ‚(Pâ€ Ï€) x

/-- Given a Generalized Bayes estimator `f`, we can define a deterministic kernel. -/
noncomputable
abbrev IsGenBayesEstimator.kernel (h : IsGenBayesEstimator â„“ P f Ï€) : Kernel ğ“§ ğ“¨ :=
  Kernel.deterministic f h.measurable

lemma IsGenBayesEstimator.bayesianRisk_eq_integral_iInf (hf : IsGenBayesEstimator â„“ P f Ï€)
    (hl : Measurable (Function.uncurry â„“)) :
    bayesianRisk â„“ P hf.kernel Ï€ = âˆ«â» x, â¨… z, âˆ«â» Î¸, â„“ Î¸ z âˆ‚(Pâ€ Ï€) x âˆ‚(P âˆ˜â‚˜ Ï€) := by
  rw [bayesianRisk_eq_integral_integral_integral hl]
  refine lintegral_congr_ae ?_
  filter_upwards [hf.property] with x hx
  rwa [Kernel.deterministic_apply,
    lintegral_dirac' _ (Measurable.lintegral_prod_left (by fun_prop))]

/-- A generalized Bayes estimator is a Bayes estimator: that is, it minimizes the Bayesian risk. -/
lemma IsGenBayesEstimator.isBayesEstimator (hf : IsGenBayesEstimator â„“ P f Ï€)
    (hl : Measurable (Function.uncurry â„“)) :
    IsBayesEstimator â„“ P hf.kernel Ï€ := by
  simp_rw [IsBayesEstimator, bayesRiskPrior]
  apply le_antisymm
  Â· rw [hf.bayesianRisk_eq_integral_iInf hl]
    simp_all [bayesianRisk_ge_lintegral_iInf_bayesInv]
  Â· refine iInf_le_of_le hf.kernel ?_
    exact iInf_le _ (Kernel.isMarkovKernel_deterministic hf.measurable)

/-- The estimation problem admits a Generalized Bayes estimator with respect to the prior `Ï€`. -/
class HasGenBayesEstimator {ğ“¨ : Type*} [MeasurableSpace ğ“¨]
    (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) [IsFiniteKernel P] (Ï€ : Measure Î˜) [IsFiniteMeasure Ï€] where
  /-- The Generalized Bayes estimator. -/
  estimator : ğ“§ â†’ ğ“¨
  isGenBayesEstimator : IsGenBayesEstimator â„“ P estimator Ï€

lemma bayesRiskPrior_eq_of_hasGenBayesEstimator
    (hl : Measurable (Function.uncurry â„“)) [h : HasGenBayesEstimator â„“ P Ï€] :
    bayesRiskPrior â„“ P Ï€ = âˆ«â» x, â¨… z, âˆ«â» Î¸, â„“ Î¸ z âˆ‚((Pâ€ Ï€) x) âˆ‚(P âˆ˜â‚˜ Ï€) := by
  rw [â† h.isGenBayesEstimator.isBayesEstimator hl,
    IsGenBayesEstimator.bayesianRisk_eq_integral_iInf _ hl]

end IsGenBayesEstimator

end ProbabilityTheory
