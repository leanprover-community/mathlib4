/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Lorenzo Luccioli
-/

import Mathlib.Probability.Decision.AuxLemmas
import Mathlib.Probability.Decision.Risk.Defs
import Mathlib.Probability.Kernel.Composition.MeasureComp
import Mathlib.Probability.Kernel.WithDensity

/-!
# Basic properties of the risk of an estimator

## Main statements

Inequalities on the risks:
* `bayesRisk_le_minimaxRisk`: the Bayes risk is less than or equal to the minimax risk.
* `lintegral_iInf_posterior_le_bayesRiskPrior`: the Bayes risk with respect to a prior is bounded
  from below by the integral over the data (with distribution `P âˆ˜â‚˜ Ï€`) of the infimum over the
  possible predictions `y` of the posterior loss `âˆ«â» Î¸, â„“ Î¸ y âˆ‚((Pâ€ Ï€) x)`:
  `âˆ«â» x, â¨… y : ğ“¨, âˆ«â» Î¸, â„“ Î¸ y âˆ‚((Pâ€ Ï€) x) âˆ‚(P âˆ˜â‚˜ Ï€) â‰¤ bayesRiskPrior â„“ P Ï€`

Data-processing inequalities: if we compose the data generating kernel `P` with a Markov kernel
`Î· : Kernel ğ“§ ğ“§'`, then the Bayes risk increases.
* `bayesRiskPrior_le_bayesRiskPrior_comp`: data-processing inequality for the Bayes risk
  with respect to a prior.
* `bayesRisk_le_bayesRisk_comp`: data-processing inequality for the Bayes risk.

-/

open MeasureTheory
open scoped ENNReal NNReal

namespace ProbabilityTheory

variable {Î˜ Î˜' ğ“§ ğ“§' ğ“¨ : Type*} {mÎ˜ : MeasurableSpace Î˜} {mÎ˜' : MeasurableSpace Î˜'}
  {mğ“§ : MeasurableSpace ğ“§} {mğ“§' : MeasurableSpace ğ“§'} {mğ“¨ : MeasurableSpace ğ“¨}
  {â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ} {P : Kernel Î˜ ğ“§} {Îº : Kernel ğ“§ ğ“¨} {Ï€ : Measure Î˜}

@[simp]
lemma bayesianRisk_of_isEmpty [IsEmpty Î˜] : bayesianRisk â„“ P Îº Ï€ = 0 := by simp [bayesianRisk]

section Zero

@[simp]
lemma bayesianRisk_zero_left (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (Îº : Kernel ğ“§ ğ“¨) (Ï€ : Measure Î˜) :
    bayesianRisk â„“ (0 : Kernel Î˜ ğ“§) Îº Ï€ = 0 := by simp [bayesianRisk]

@[simp]
lemma bayesianRisk_zero_right (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) (Ï€ : Measure Î˜) :
    bayesianRisk â„“ P (0 : Kernel ğ“§ ğ“¨) Ï€ = 0 := by simp [bayesianRisk]

@[simp]
lemma bayesianRisk_zero_prior (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) (Îº : Kernel ğ“§ ğ“¨) :
    bayesianRisk â„“ P Îº 0 = 0 := by simp [bayesianRisk]

instance [IsEmpty ğ“¨] : Subsingleton (Kernel ğ“§ ğ“¨) where
  allEq Îº Î· := by
    ext a s hs
    suffices s = âˆ… by simp [this]
    exact Set.eq_empty_of_isEmpty s

@[simp]
lemma bayesianRisk_of_isEmpty' [IsEmpty ğ“§] : bayesianRisk â„“ P Îº Ï€ = 0 := by
  have : P = 0 := Subsingleton.elim P 0
  simp [this]

instance [IsEmpty ğ“§] (Îº : Kernel ğ“§ ğ“¨) : IsMarkovKernel Îº where
  isProbabilityMeasure := by simp

lemma not_isMarkovKernel_zero [Nonempty ğ“§] : Â¬ IsMarkovKernel (0 : Kernel ğ“§ ğ“¨) := by
  by_contra h
  let x : ğ“§ := Nonempty.some inferInstance
  have h1 : (0 : Measure ğ“¨) .univ = 1 := (h.isProbabilityMeasure x).measure_univ
  simp only [Measure.coe_zero, Pi.zero_apply, zero_ne_one] at h1

@[simp]
lemma bayesRiskPrior_of_isEmpty_of_isEmpty (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) (Ï€ : Measure Î˜)
    [IsEmpty ğ“§] [IsEmpty ğ“¨] :
    bayesRiskPrior â„“ P Ï€ = 0 := by
  simp only [bayesRiskPrior]
  rw [iInf_subtype']
  have : Nonempty (Subtype (@IsMarkovKernel ğ“§ ğ“¨ mğ“§ mğ“¨)) := by
    simp only [nonempty_subtype]
    exact âŸ¨0, inferInstanceâŸ©
  simp

@[simp]
lemma bayesRiskPrior_of_nonempty_of_isEmpty (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) (Ï€ : Measure Î˜)
    [Nonempty ğ“§] [IsEmpty ğ“¨] :
    bayesRiskPrior â„“ P Ï€ = âˆ := by
  simp only [bayesRiskPrior]
  rw [iInf_subtype']
  have : IsEmpty (Subtype (@IsMarkovKernel ğ“§ ğ“¨ mğ“§ mğ“¨)) := by
    simp only [isEmpty_subtype]
    intro Îº
    rw [Subsingleton.allEq Îº 0]
    exact not_isMarkovKernel_zero
  simp

@[simp]
lemma bayesRiskPrior_zero_left (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (Ï€ : Measure Î˜) [Nonempty ğ“¨] :
    bayesRiskPrior â„“ (0 : Kernel Î˜ ğ“§) Ï€ = 0 := by
  simp only [bayesRiskPrior, bayesianRisk_zero_left]
  rw [iInf_subtype']
  simp

@[simp]
lemma bayesRiskPrior_zero_right (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) [Nonempty ğ“¨] :
    bayesRiskPrior â„“ P (0 : Measure Î˜) = 0 := by
  simp only [bayesRiskPrior, bayesianRisk_zero_prior]
  rw [iInf_subtype']
  simp

@[simp]
lemma bayesRiskPrior_of_isEmpty_of_nonempty (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) (Ï€ : Measure Î˜)
    [IsEmpty ğ“§] [Nonempty ğ“¨] :
    bayesRiskPrior â„“ P Ï€ = 0 := by
  have : P = 0 := Subsingleton.elim P 0
  simp [this]

end Zero

section Const

lemma bayesianRisk_const (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (Î¼ : Measure ğ“§) (Îº : Kernel ğ“§ ğ“¨) (Ï€ : Measure Î˜) :
    bayesianRisk â„“ (Kernel.const Î˜ Î¼) Îº Ï€ = âˆ«â» Î¸, âˆ«â» y, â„“ Î¸ y âˆ‚(Îº âˆ˜â‚˜ Î¼) âˆ‚Ï€ := by
  simp [bayesianRisk]

lemma bayesianRisk_const' (hl : Measurable (Function.uncurry â„“)) (Î¼ : Measure ğ“§) [SFinite Î¼]
    (Îº : Kernel ğ“§ ğ“¨) [IsSFiniteKernel Îº] (Ï€ : Measure Î˜) [SFinite Ï€] :
    bayesianRisk â„“ (Kernel.const Î˜ Î¼) Îº Ï€ = âˆ«â» y, âˆ«â» Î¸, â„“ Î¸ y âˆ‚Ï€ âˆ‚(Îº âˆ˜â‚˜ Î¼) := by
  rw [bayesianRisk_const, lintegral_lintegral_swap (by fun_prop)]

lemma bayesianRisk_const_right (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) (Î½ : Measure ğ“¨) (Ï€ : Measure Î˜) :
    bayesianRisk â„“ P (Kernel.const ğ“§ Î½) Ï€ = âˆ«â» Î¸, P Î¸ .univ * âˆ«â» y, â„“ Î¸ y âˆ‚Î½ âˆ‚Ï€ := by
  simp [bayesianRisk, Kernel.const_comp]

lemma bayesRiskPrior_le_inf' (hl : Measurable (Function.uncurry â„“)) (P : Kernel Î˜ ğ“§)
    (Ï€ : Measure Î˜) :
    bayesRiskPrior â„“ P Ï€ â‰¤ â¨… y : ğ“¨, âˆ«â» Î¸, â„“ Î¸ y * P Î¸ .univ âˆ‚Ï€ := by
  simp_rw [le_iInf_iff, bayesRiskPrior]
  refine fun y â†¦ iInf_le_of_le (Kernel.const _ (Measure.dirac y)) ?_
  simp only [iInf_pos, bayesianRisk_const_right, mul_comm]
  gcongr with Î¸
  rw [lintegral_dirac' _ (by fun_prop)]

lemma bayesRiskPrior_le_inf (hl : Measurable (Function.uncurry â„“)) (P : Kernel Î˜ ğ“§)
    (Ï€ : Measure Î˜) [IsMarkovKernel P] :
    bayesRiskPrior â„“ P Ï€ â‰¤ â¨… y : ğ“¨, âˆ«â» Î¸, â„“ Î¸ y âˆ‚Ï€ :=
  (bayesRiskPrior_le_inf' hl P Ï€).trans_eq (by simp)

lemma bayesRiskPrior_lt_top [Nonempty ğ“¨] (hl : Measurable (Function.uncurry â„“)) (P : Kernel Î˜ ğ“§)
    [IsFiniteKernel P] (Ï€ : Measure Î˜) [IsFiniteMeasure Ï€] {C : â„â‰¥0} (hâ„“C : âˆ€ Î¸ y, â„“ Î¸ y â‰¤ C) :
    bayesRiskPrior â„“ P Ï€ < âˆ := by
  refine (bayesRiskPrior_le_inf' hl P Ï€).trans_lt ?_
  have y : ğ“¨ := Nonempty.some inferInstance
  refine (iInf_le _ y).trans_lt ?_
  calc âˆ«â» Î¸, â„“ Î¸ y * (P Î¸) Set.univ âˆ‚Ï€
  _ â‰¤ âˆ«â» Î¸, C * (IsFiniteKernel.bound P) âˆ‚Ï€ := by
    gcongr with Î¸
    Â· exact hâ„“C Î¸ y
    Â· exact Kernel.measure_le_bound P Î¸ Set.univ
  _ < âŠ¤ := by simp [ENNReal.mul_lt_top_iff, IsFiniteKernel.bound_lt_top P]

lemma bayesRiskPrior_const''' (hl : Measurable (Function.uncurry â„“))
    (Î¼ : Measure ğ“§) [SFinite Î¼] (Ï€ : Measure Î˜) [SFinite Ï€]
    (hl_pos : Î¼ .univ = âˆ â†’ â¨… y, âˆ«â» Î¸, â„“ Î¸ y âˆ‚Ï€ = 0 â†’ âˆƒ y, âˆ«â» Î¸, â„“ Î¸ y âˆ‚Ï€ = 0)
    (h_zero : Î¼ = 0 â†’ Nonempty ğ“¨) :
    bayesRiskPrior â„“ (Kernel.const Î˜ Î¼) Ï€ = â¨… y : ğ“¨, âˆ«â» Î¸, â„“ Î¸ y * Î¼ .univ âˆ‚Ï€ := by
  refine le_antisymm ?_ ?_
  Â· exact (bayesRiskPrior_le_inf' hl _ _).trans_eq (by simp)
  Â· simp_rw [bayesRiskPrior, le_iInf_iff]
    intro Îº hÎº
    rw [bayesianRisk_const' hl]
    refine le_trans ?_ (iInf_mul_le_lintegral (Îº âˆ˜â‚˜ Î¼) (fun y â†¦ âˆ«â» Î¸, â„“ Î¸ y âˆ‚Ï€))
    simp only [Measure.comp_apply_univ]
    rw [ENNReal.iInf_mul' hl_pos (fun hÎ¼ â†¦ h_zero (by simpa using hÎ¼))]
    gcongr with y
    rw [lintegral_mul_const]
    fun_prop

lemma bayesRiskPrior_const'' (hl : Measurable (Function.uncurry â„“))
    (Î¼ : Measure ğ“§) [NeZero Î¼] [IsFiniteMeasure Î¼] (Ï€ : Measure Î˜) [SFinite Ï€] :
    bayesRiskPrior â„“ (Kernel.const Î˜ Î¼) Ï€ = â¨… y : ğ“¨, âˆ«â» Î¸, â„“ Î¸ y * Î¼ .univ âˆ‚Ï€ :=
  bayesRiskPrior_const''' hl Î¼ Ï€ (by simp) (by simp [NeZero.out])

lemma bayesRiskPrior_const' [Nonempty ğ“¨] (hl : Measurable (Function.uncurry â„“))
    (Î¼ : Measure ğ“§) [IsFiniteMeasure Î¼] (Ï€ : Measure Î˜) [SFinite Ï€] :
    bayesRiskPrior â„“ (Kernel.const Î˜ Î¼) Ï€ = â¨… y : ğ“¨, âˆ«â» Î¸, â„“ Î¸ y * Î¼ .univ âˆ‚Ï€ :=
  bayesRiskPrior_const''' hl Î¼ Ï€ (by simp) (fun _ â†¦ inferInstance)

lemma bayesRiskPrior_const (hl : Measurable (Function.uncurry â„“))
    (Î¼ : Measure ğ“§) [IsProbabilityMeasure Î¼] (Ï€ : Measure Î˜) [SFinite Ï€] :
    bayesRiskPrior â„“ (Kernel.const Î˜ Î¼) Ï€ = â¨… y : ğ“¨, âˆ«â» Î¸, â„“ Î¸ y âˆ‚Ï€ := by
  simp [bayesRiskPrior_const'' hl Î¼ Ï€]

end Const

lemma bayesRiskPrior_discard (hl : Measurable (Function.uncurry â„“)) (Ï€ : Measure Î˜) [SFinite Ï€] :
    bayesRiskPrior â„“ (Kernel.discard Î˜) Ï€ = â¨… y : ğ“¨, âˆ«â» Î¸, â„“ Î¸ y âˆ‚Ï€ := by
  have : Kernel.discard Î˜ = Kernel.const Î˜ (Measure.dirac ()) := by ext; simp
  rw [this, bayesRiskPrior_const hl]

lemma bayesRiskPrior_eq_iInf_measure_of_subsingleton [Subsingleton ğ“§] [Nonempty ğ“¨] :
    bayesRiskPrior â„“ P Ï€
      = â¨… (Î¼ : Measure ğ“¨) (_ : IsProbabilityMeasure Î¼), bayesianRisk â„“ P (Kernel.const ğ“§ Î¼) Ï€ := by
  rcases isEmpty_or_nonempty ğ“§ with hX | hX
  Â· rw [iInf_subtype']
    have : Nonempty {Î¼ : Measure ğ“¨ // IsProbabilityMeasure Î¼} := by
      simp only [nonempty_subtype]
      exact âŸ¨Measure.dirac (Nonempty.some inferInstance), inferInstanceâŸ©
    simp
  obtain x := Nonempty.some hX
  rw [bayesRiskPrior, iInf_subtype', iInf_subtype']
  let e : {Îº : Kernel ğ“§ ğ“¨ // IsMarkovKernel Îº} â‰ƒ {Î¼ : Measure ğ“¨ // IsProbabilityMeasure Î¼} :=
    { toFun Îº := âŸ¨Îº.1 x, by have := Îº.2.isProbabilityMeasure x; infer_instanceâŸ©
      invFun Î¼ := âŸ¨Kernel.const ğ“§ Î¼, by have := Î¼.2; infer_instanceâŸ©
      left_inv Îº := by ext y; simp only [Kernel.const_apply, Subsingleton.allEq x y]
      right_inv Î¼ := by simp }
  rw [â† Equiv.iInf_comp e.symm]
  congr with Î¼

lemma bayesRiskPrior_of_subsingleton [Subsingleton ğ“§] [Nonempty ğ“¨] [SFinite Ï€]
    (hl : Measurable (Function.uncurry â„“)) :
    bayesRiskPrior â„“ P Ï€ = â¨… y : ğ“¨, âˆ«â» Î¸, â„“ Î¸ y * P Î¸ .univ âˆ‚Ï€ := by
  refine le_antisymm (bayesRiskPrior_le_inf' hl _ _) ?_
  rw [bayesRiskPrior_eq_iInf_measure_of_subsingleton]
  simp only [bayesianRisk_const_right, le_iInf_iff]
  refine fun Î¼ hÎ¼ â†¦ (iInf_le_lintegral Î¼ _).trans_eq ?_
  rw [lintegral_lintegral_swap]
  Â· congr with Î¸
    rw [lintegral_mul_const _ (by fun_prop), mul_comm]
  Â· have := P.measurable_coe .univ
    fun_prop

lemma bayesRiskPrior_eq_bayesRiskPrior_discard_of_subsingleton [Subsingleton ğ“§] [Nonempty ğ“¨]
    [IsMarkovKernel P] [SFinite Ï€] (hl : Measurable (Function.uncurry â„“)) :
    bayesRiskPrior â„“ P Ï€ = bayesRiskPrior â„“ (Kernel.discard Î˜) Ï€ := by
  simp [bayesRiskPrior_of_subsingleton hl]

lemma bayesianRisk_withDensity (hl : Measurable (Function.uncurry â„“))
    (P : Kernel Î˜ ğ“§) [IsSFiniteKernel P] (Îº : Kernel ğ“§ ğ“¨) [IsSFiniteKernel Îº]
    (Ï€ : Measure Î˜) [SFinite Ï€] {f : Î˜ â†’ â„â‰¥0âˆ} (hf : Measurable f) :
    bayesianRisk â„“ (P.withDensity (fun Î¸ _ â†¦ f Î¸)) Îº Ï€ = bayesianRisk â„“ P Îº (Ï€.withDensity f) := by
  simp only [bayesianRisk]
  rw [lintegral_withDensity_eq_lintegral_mul _ hf (by fun_prop)]
  congr with Î¸
  rw [Kernel.comp_apply, Kernel.withDensity_apply _ (by fun_prop), Pi.mul_apply, Kernel.comp_apply]
  simp

lemma bayesRiskPrior_withDensity (hl : Measurable (Function.uncurry â„“))
    (P : Kernel Î˜ ğ“§) [IsSFiniteKernel P] (Ï€ : Measure Î˜) [SFinite Ï€]
    {f : Î˜ â†’ â„â‰¥0âˆ} (hf : Measurable f) :
    bayesRiskPrior â„“ (P.withDensity (fun Î¸ _ â†¦ f Î¸)) Ï€ = bayesRiskPrior â„“ P (Ï€.withDensity f) := by
  simp_rw [bayesRiskPrior]
  congr! 3 with Îº hÎº
  rw [bayesianRisk_withDensity hl P Îº Ï€ hf]

section BayesRiskLeMinimaxRisk

lemma bayesianRisk_le_iSup_risk (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) (Îº : Kernel ğ“§ ğ“¨)
    (Ï€ : Measure Î˜) [IsProbabilityMeasure Ï€] :
    bayesianRisk â„“ P Îº Ï€ â‰¤ â¨† Î¸, âˆ«â» y, â„“ Î¸ y âˆ‚((Îº âˆ˜â‚– P) Î¸) := by
  rw [bayesianRisk]
  calc âˆ«â» Î¸, âˆ«â» y, â„“ Î¸ y âˆ‚((Îº âˆ˜â‚– P) Î¸) âˆ‚Ï€
  _ â‰¤ â¨† Î¸, âˆ«â» y, â„“ Î¸ y âˆ‚((Îº âˆ˜â‚– P) Î¸) := lintegral_le_iSup _ _

lemma bayesRiskPrior_le_bayesianRisk (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) (Îº : Kernel ğ“§ ğ“¨)
    (Ï€ : Measure Î˜) [hÎº : IsMarkovKernel Îº] :
    bayesRiskPrior â„“ P Ï€ â‰¤ bayesianRisk â„“ P Îº Ï€ := iInfâ‚‚_le Îº hÎº

lemma bayesRiskPrior_le_minimaxRisk (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§)
    (Ï€ : Measure Î˜) [IsProbabilityMeasure Ï€] :
    bayesRiskPrior â„“ P Ï€ â‰¤ minimaxRisk â„“ P :=
  iInfâ‚‚_mono fun _ _ â†¦ bayesianRisk_le_iSup_risk _ _ _ _

lemma bayesRisk_le_minimaxRisk (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) :
    bayesRisk â„“ P â‰¤ minimaxRisk â„“ P := by
  simp only [bayesRisk, iSup_le_iff]
  exact fun _ _ â†¦ bayesRiskPrior_le_minimaxRisk _ _ _

end BayesRiskLeMinimaxRisk

section Compositions

/-- **Data processing inequality** for the Bayes risk with respect to a prior: composition of the
data generating kernel by a Markov kernel increases the risk. -/
lemma bayesRiskPrior_le_bayesRiskPrior_comp (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§)
    (Ï€ : Measure Î˜) (Î· : Kernel ğ“§ ğ“§') [IsMarkovKernel Î·] :
    bayesRiskPrior â„“ P Ï€ â‰¤ bayesRiskPrior â„“ (Î· âˆ˜â‚– P) Ï€ := by
  simp only [bayesRiskPrior, bayesianRisk, le_iInf_iff]
  intro Îº hÎº
  rw [â† Îº.comp_assoc Î·]
  exact iInf_le_of_le (Îº âˆ˜â‚– Î·) (iInf_le_of_le inferInstance le_rfl)

lemma bayesRiskPrior_compProd_le_bayesRiskPrior (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§)
    [IsSFiniteKernel P] (Ï€ : Measure Î˜) (Î· : Kernel (Î˜ Ã— ğ“§) ğ“§') [IsMarkovKernel Î·] :
    bayesRiskPrior â„“ (P âŠ—â‚– Î·) Ï€ â‰¤ bayesRiskPrior â„“ P Ï€ := by
  have : P = (Kernel.deterministic Prod.fst (by fun_prop)) âˆ˜â‚– (P âŠ—â‚– Î·) := by
    rw [Kernel.deterministic_comp_eq_map, â† Kernel.fst_eq, Kernel.fst_compProd]
  conv_rhs => rw [this]
  exact bayesRiskPrior_le_bayesRiskPrior_comp _ _ _ _

lemma bayesianRisk_comap_measurableEquiv (hl : Measurable (Function.uncurry â„“)) (P : Kernel Î˜ ğ“§)
    [IsSFiniteKernel P] (Îº : Kernel ğ“§ ğ“¨) [IsSFiniteKernel Îº] (Ï€ : Measure Î˜) (e : Î˜' â‰ƒáµ Î˜) :
    bayesianRisk (fun Î¸ â†¦ â„“ (e Î¸)) (P.comap e e.measurable) Îº (Ï€.comap e)
      = bayesianRisk â„“ P Îº Ï€ := by
  simp only [bayesianRisk]
  rw [â† MeasurableEquiv.map_symm, lintegral_map (by fun_prop) e.symm.measurable]
  congr with Î¸
  congr
  Â· ext s hs
    simp [Îº.comp_apply' _ _ hs, Kernel.comap_apply]
  Â· simp

lemma bayesRiskPrior_comap_measurableEquiv (hl : Measurable (Function.uncurry â„“)) (P : Kernel Î˜ ğ“§)
    [IsSFiniteKernel P] (Ï€ : Measure Î˜) (e : Î˜' â‰ƒáµ Î˜) :
    bayesRiskPrior (fun Î¸ â†¦ â„“ (e Î¸)) (P.comap e e.measurable) (Ï€.comap e)
      = bayesRiskPrior â„“ P Ï€ := by
  simp only [bayesRiskPrior, bayesianRisk_comap_measurableEquiv hl P _ Ï€ e]

/-- **Data processing inequality** for the Bayes risk: composition of the
data generating kernel by a Markov kernel increases the risk. -/
lemma bayesRisk_le_bayesRisk_comp (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§)
    (Î· : Kernel ğ“§ ğ“§') [IsMarkovKernel Î·] :
    bayesRisk â„“ P â‰¤ bayesRisk â„“ (Î· âˆ˜â‚– P) :=
  iSupâ‚‚_mono fun _ _ â†¦ bayesRiskPrior_le_bayesRiskPrior_comp _ _ _ _

lemma bayesRisk_compProd_le_bayesRisk (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§)
    [IsSFiniteKernel P] (Î· : Kernel (Î˜ Ã— ğ“§) ğ“§') [IsMarkovKernel Î·] :
    bayesRisk â„“ (P âŠ—â‚– Î·) â‰¤ bayesRisk â„“ P :=
  iSupâ‚‚_mono fun _ _ â†¦ bayesRiskPrior_compProd_le_bayesRiskPrior _ _ _ _

end Compositions

end ProbabilityTheory
