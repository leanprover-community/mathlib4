/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Lorenzo Luccioli
-/

import Mathlib.Probability.Decision.Risk.Defs
import Mathlib.Probability.Kernel.Composition.MeasureComp

/-!
# Basic properties of the risk of an estimator

## Main statements

* `iSup_bayesRisk_le_minimaxRisk`: the maximal Bayes risk is less than or equal to the minimax risk.
* `bayesRisk_le_bayesRisk_comp`: data-processing inequality for the Bayes risk with respect to a
  prior: if we compose the data generating kernel `P` with a Markov kernel, then the Bayes risk
  increases.
* `bayesRisk_le_iInf`: for `P` a Markov kernel, the Bayes risk is less than `â¨… y, âˆ«â» Î¸, â„“ Î¸ y âˆ‚Ï€`.

In several cases, there is no information in the data about the parameter and the Bayes risk takes
its maximal value.
* `bayesRisk_const`: if the data generating kernel is constant, then the Bayes risk is equal to
  `â¨… y, âˆ«â» Î¸, â„“ Î¸ y âˆ‚Ï€`.
* `bayesRisk_of_subsingleton`: if the observation space is a subsingleton, then the Bayes risk is
  equal to `â¨… y, âˆ«â» Î¸, â„“ Î¸ y âˆ‚Ï€`.

## TODO

In many cases, the maximal Bayes risk and the minimax risk are equal
(by a so-called minimax theorem).

-/

open MeasureTheory Function
open scoped ENNReal NNReal

namespace ProbabilityTheory

variable {Î˜ ğ“§ ğ“§' ğ“¨ : Type*} {mÎ˜ : MeasurableSpace Î˜}
  {mğ“§ : MeasurableSpace ğ“§} {mğ“§' : MeasurableSpace ğ“§'} {mğ“¨ : MeasurableSpace ğ“¨}
  {â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ} {P : Kernel Î˜ ğ“§} {Îº : Kernel ğ“§ ğ“¨} {Ï€ : Measure Î˜}

section BayesRiskLeMinimaxRisk

lemma avgRisk_le_iSup_risk (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) (Îº : Kernel ğ“§ ğ“¨)
    (Ï€ : Measure Î˜) [IsProbabilityMeasure Ï€] :
    avgRisk â„“ P Îº Ï€ â‰¤ â¨† Î¸, âˆ«â» y, â„“ Î¸ y âˆ‚((Îº âˆ˜â‚– P) Î¸) := lintegral_le_iSup _

lemma bayesRisk_le_avgRisk (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) (Îº : Kernel ğ“§ ğ“¨)
    (Ï€ : Measure Î˜) [hÎº : IsMarkovKernel Îº] :
    bayesRisk â„“ P Ï€ â‰¤ avgRisk â„“ P Îº Ï€ := iInfâ‚‚_le Îº hÎº

lemma bayesRisk_le_minimaxRisk (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§)
    (Ï€ : Measure Î˜) [IsProbabilityMeasure Ï€] :
    bayesRisk â„“ P Ï€ â‰¤ minimaxRisk â„“ P := iInfâ‚‚_mono fun _ _ â†¦ avgRisk_le_iSup_risk _ _ _ _

/-- The maximal Bayes risk is less than or equal to the minimax risk. -/
lemma iSup_bayesRisk_le_minimaxRisk (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) :
    â¨† (Ï€ : Measure Î˜) (_ : IsProbabilityMeasure Ï€), bayesRisk â„“ P Ï€
      â‰¤ minimaxRisk â„“ P := iSupâ‚‚_le fun _ _ â†¦ bayesRisk_le_minimaxRisk _ _ _

end BayesRiskLeMinimaxRisk

section Const

/-- See `avgRisk_const_left'` for a similar result with integrals swapped. -/
lemma avgRisk_const_left (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (Î¼ : Measure ğ“§) (Îº : Kernel ğ“§ ğ“¨) (Ï€ : Measure Î˜) :
    avgRisk â„“ (Kernel.const Î˜ Î¼) Îº Ï€ = âˆ«â» Î¸, âˆ«â» y, â„“ Î¸ y âˆ‚(Îº âˆ˜â‚˜ Î¼) âˆ‚Ï€ := by
  simp [avgRisk]

/-- See `avgRisk_const_left` for a similar result with integrals swapped. -/
lemma avgRisk_const_left' (hl : Measurable (uncurry â„“)) (Î¼ : Measure ğ“§) [SFinite Î¼]
    (Îº : Kernel ğ“§ ğ“¨) [IsSFiniteKernel Îº] (Ï€ : Measure Î˜) [SFinite Ï€] :
    avgRisk â„“ (Kernel.const Î˜ Î¼) Îº Ï€ = âˆ«â» y, âˆ«â» Î¸, â„“ Î¸ y âˆ‚Ï€ âˆ‚(Îº âˆ˜â‚˜ Î¼) := by
  rw [avgRisk_const_left, lintegral_lintegral_swap (by fun_prop)]

/-- See `avgRisk_const_right` for a simpler result when `P` is a Markov kernel. -/
lemma avgRisk_const_right' (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) (Î½ : Measure ğ“¨) (Ï€ : Measure Î˜) :
    avgRisk â„“ P (Kernel.const ğ“§ Î½) Ï€ = âˆ«â» Î¸, P Î¸ .univ * âˆ«â» y, â„“ Î¸ y âˆ‚Î½ âˆ‚Ï€ := by
  simp [avgRisk, Kernel.const_comp]

/-- See `avgRisk_const_right'` for a similar result when `P` is not a Markov kernel. -/
lemma avgRisk_const_right (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) [IsMarkovKernel P]
    (Î½ : Measure ğ“¨) (Ï€ : Measure Î˜) :
    avgRisk â„“ P (Kernel.const ğ“§ Î½) Ï€ = âˆ«â» Î¸, âˆ«â» y, â„“ Î¸ y âˆ‚Î½ âˆ‚Ï€ := by
  simp [avgRisk_const_right']

/-- See `bayesRisk_le_iInf` for a simpler result when `P` is a Markov kernel. -/
lemma bayesRisk_le_iInf' (hl : Measurable (uncurry â„“)) (P : Kernel Î˜ ğ“§) (Ï€ : Measure Î˜) :
    bayesRisk â„“ P Ï€ â‰¤ â¨… y, âˆ«â» Î¸, â„“ Î¸ y * P Î¸ .univ âˆ‚Ï€ := by
  simp_rw [le_iInf_iff, bayesRisk]
  refine fun y â†¦ iInf_le_of_le (Kernel.const _ (Measure.dirac y)) ?_
  simp only [iInf_pos, avgRisk_const_right', mul_comm]
  gcongr with Î¸
  rw [lintegral_dirac' _ (by fun_prop)]

/-- See `bayesRisk_le_iInf'` for a similar result when `P` is not a Markov kernel. -/
lemma bayesRisk_le_iInf (hl : Measurable (uncurry â„“)) (P : Kernel Î˜ ğ“§) [IsMarkovKernel P]
    (Ï€ : Measure Î˜) :
    bayesRisk â„“ P Ï€ â‰¤ â¨… y, âˆ«â» Î¸, â„“ Î¸ y âˆ‚Ï€ :=
  (bayesRisk_le_iInf' hl P Ï€).trans_eq (by simp)

lemma bayesRisk_const' (hl : Measurable (uncurry â„“))
    (Î¼ : Measure ğ“§) [SFinite Î¼] (Ï€ : Measure Î˜) [SFinite Ï€]
    (hl_pos : Î¼ .univ = âˆ â†’ â¨… y, âˆ«â» Î¸, â„“ Î¸ y âˆ‚Ï€ = 0 â†’ âˆƒ y, âˆ«â» Î¸, â„“ Î¸ y âˆ‚Ï€ = 0)
    (h_zero : Î¼ = 0 â†’ Nonempty ğ“¨) :
    bayesRisk â„“ (Kernel.const Î˜ Î¼) Ï€ = â¨… y, âˆ«â» Î¸, â„“ Î¸ y * Î¼ .univ âˆ‚Ï€ := by
  refine le_antisymm ((bayesRisk_le_iInf' hl _ _).trans_eq (by simp)) ?_
  simp_rw [bayesRisk, le_iInf_iff]
  intro Îº hÎº
  rw [avgRisk_const_left' hl]
  refine le_trans ?_ (iInf_mul_le_lintegral (fun y â†¦ âˆ«â» Î¸, â„“ Î¸ y âˆ‚Ï€))
  rw [Measure.comp_apply_univ, ENNReal.iInf_mul' hl_pos (fun hÎ¼ â†¦ h_zero (by simpa using hÎ¼))]
  gcongr with y
  rw [lintegral_mul_const]
  fun_prop

lemma bayesRisk_const_of_neZero (hl : Measurable (uncurry â„“))
    (Î¼ : Measure ğ“§) [NeZero Î¼] [IsFiniteMeasure Î¼] (Ï€ : Measure Î˜) [SFinite Ï€] :
    bayesRisk â„“ (Kernel.const Î˜ Î¼) Ï€ = â¨… y, âˆ«â» Î¸, â„“ Î¸ y * Î¼ .univ âˆ‚Ï€ :=
  bayesRisk_const' hl Î¼ Ï€ (by simp) (by simp [NeZero.out])

lemma bayesRisk_const_of_nonempty [Nonempty ğ“¨] (hl : Measurable (uncurry â„“))
    (Î¼ : Measure ğ“§) [IsFiniteMeasure Î¼] (Ï€ : Measure Î˜) [SFinite Ï€] :
    bayesRisk â„“ (Kernel.const Î˜ Î¼) Ï€ = â¨… y, âˆ«â» Î¸, â„“ Î¸ y * Î¼ .univ âˆ‚Ï€ :=
  bayesRisk_const' hl Î¼ Ï€ (by simp) (fun _ â†¦ inferInstance)

lemma bayesRisk_const (hl : Measurable (uncurry â„“))
    (Î¼ : Measure ğ“§) [IsProbabilityMeasure Î¼] (Ï€ : Measure Î˜) [SFinite Ï€] :
    bayesRisk â„“ (Kernel.const Î˜ Î¼) Ï€ = â¨… y, âˆ«â» Î¸, â„“ Î¸ y âˆ‚Ï€ := by
  simp [bayesRisk_const_of_neZero hl Î¼ Ï€]

end Const

section Bounds

/-- See `avgRisk_le_mul` for the usual case in which `Ï€` is a probability measure and the kernels
are Markov. -/
lemma avgRisk_le_mul' (P : Kernel Î˜ ğ“§) (Îº : Kernel ğ“§ ğ“¨) (Ï€ : Measure Î˜)
    {C : â„â‰¥0} (hâ„“C : âˆ€ Î¸ y, â„“ Î¸ y â‰¤ C) :
    avgRisk â„“ P Îº Ï€ â‰¤ C * Îº.bound * P.bound * Ï€ Set.univ :=
  calc âˆ«â» Î¸, âˆ«â» y, â„“ Î¸ y âˆ‚(Îº âˆ˜â‚– P) Î¸ âˆ‚Ï€
  _ â‰¤ âˆ«â» Î¸, âˆ«â» y, C âˆ‚(Îº âˆ˜â‚– P) Î¸ âˆ‚Ï€ := by gcongr with Î¸ y; exact hâ„“C Î¸ y
  _ = âˆ«â» Î¸, C * âˆ«â» x, Îº x .univ âˆ‚P Î¸ âˆ‚Ï€ := by simp [Kernel.comp_apply' _ _ _ .univ]
  _ â‰¤ âˆ«â» Î¸, C * âˆ«â» x, Îº.bound âˆ‚P Î¸ âˆ‚Ï€ := by
    gcongr with Î¸ x
    exact Kernel.measure_le_bound Îº x Set.univ
  _ â‰¤ âˆ«â» Î¸, C * Îº.bound * P.bound âˆ‚Ï€ := by
    conv_lhs => simp only [lintegral_const, â† mul_assoc]
    gcongr with Î¸
    exact Kernel.measure_le_bound P Î¸ Set.univ
  _ = C * Îº.bound * P.bound * Ï€ Set.univ := by simp

lemma avgRisk_le_mul (P : Kernel Î˜ ğ“§) [IsMarkovKernel P] (Îº : Kernel ğ“§ ğ“¨) [IsMarkovKernel Îº]
    (Ï€ : Measure Î˜) [IsProbabilityMeasure Ï€] {C : â„â‰¥0} (hâ„“C : âˆ€ Î¸ y, â„“ Î¸ y â‰¤ C) :
    avgRisk â„“ P Îº Ï€ â‰¤ C := by
  refine (avgRisk_le_mul' P Îº Ï€ hâ„“C).trans ?_
  rcases isEmpty_or_nonempty Î˜
  Â· simp
  Â· rcases isEmpty_or_nonempty ğ“§ <;> simp

/-- For a bounded loss, the Bayes risk with respect to a prior is bounded by a constant.
See `bayesRisk_le_mul` for the usual cases where all measures are probability measures. -/
lemma bayesRisk_le_mul' [hğ“¨ : Nonempty ğ“¨] (P : Kernel Î˜ ğ“§) (Ï€ : Measure Î˜)
    {C : â„â‰¥0} (hâ„“C : âˆ€ Î¸ y, â„“ Î¸ y â‰¤ C) :
    bayesRisk â„“ P Ï€ â‰¤ C * P.bound * Ï€ Set.univ := by
  refine (bayesRisk_le_avgRisk â„“ P (Kernel.const ğ“§ (Measure.dirac hğ“¨.some)) Ï€).trans ?_
  refine (avgRisk_le_mul' P (Kernel.const ğ“§ (Measure.dirac hğ“¨.some)) Ï€ hâ„“C).trans ?_
  rcases isEmpty_or_nonempty ğ“§ <;> simp

/-- For a bounded loss, the Bayes risk with respect to a prior is bounded by a constant. -/
lemma bayesRisk_le_mul [Nonempty ğ“¨] (P : Kernel Î˜ ğ“§) [IsMarkovKernel P]
    (Ï€ : Measure Î˜) [IsProbabilityMeasure Ï€] {C : â„â‰¥0} (hâ„“C : âˆ€ Î¸ y, â„“ Î¸ y â‰¤ C) :
    bayesRisk â„“ P Ï€ â‰¤ C := by
  refine (bayesRisk_le_mul' P Ï€ hâ„“C).trans ?_
  rcases isEmpty_or_nonempty Î˜ <;> simp

/-- For a bounded loss, the Bayes risk with respect to a prior is finite. -/
lemma bayesRisk_lt_top [Nonempty ğ“¨] (P : Kernel Î˜ ğ“§)
    [IsFiniteKernel P] (Ï€ : Measure Î˜) [IsFiniteMeasure Ï€] {C : â„â‰¥0} (hâ„“C : âˆ€ Î¸ y, â„“ Î¸ y â‰¤ C) :
    bayesRisk â„“ P Ï€ < âˆ := by
  refine (bayesRisk_le_mul' P Ï€ hâ„“C).trans_lt ?_
  simp [ENNReal.mul_lt_top_iff, P.bound_lt_top]

end Bounds

lemma bayesRisk_discard (hl : Measurable (uncurry â„“)) (Ï€ : Measure Î˜) [SFinite Ï€] :
    bayesRisk â„“ (Kernel.discard Î˜) Ï€ = â¨… y, âˆ«â» Î¸, â„“ Î¸ y âˆ‚Ï€ := by
  rw [Kernel.discard_eq_const, bayesRisk_const hl]

section Subsingleton

variable [Subsingleton ğ“§] [Nonempty ğ“¨]

lemma bayesRisk_eq_iInf_measure_of_subsingleton :
    bayesRisk â„“ P Ï€
      = â¨… (Î¼ : Measure ğ“¨) (_ : IsProbabilityMeasure Î¼), avgRisk â„“ P (Kernel.const ğ“§ Î¼) Ï€ := by
  rcases isEmpty_or_nonempty ğ“§ with hX | hX
  Â· simp [iInf_subtype']
  obtain x := hX.some
  rw [bayesRisk, iInf_subtype', iInf_subtype']
  let e : {Îº : Kernel ğ“§ ğ“¨ // IsMarkovKernel Îº} â‰ƒ {Î¼ : Measure ğ“¨ // IsProbabilityMeasure Î¼} :=
    { toFun Îº := âŸ¨Îº.1 x, Îº.2.isProbabilityMeasure xâŸ©
      invFun Î¼ := âŸ¨Kernel.const ğ“§ Î¼, âŸ¨fun _ â†¦ Î¼.2âŸ©âŸ©
      left_inv Îº := by ext y; simp only [Kernel.const_apply, Subsingleton.elim x y]
      right_inv Î¼ := by simp }
  rw [â† Equiv.iInf_comp e.symm]
  rfl

lemma bayesRisk_of_subsingleton' [SFinite Ï€] (hl : Measurable (uncurry â„“)) :
    bayesRisk â„“ P Ï€ = â¨… y, âˆ«â» Î¸, â„“ Î¸ y * P Î¸ .univ âˆ‚Ï€ := by
  refine le_antisymm (bayesRisk_le_iInf' hl _ _) ?_
  rw [bayesRisk_eq_iInf_measure_of_subsingleton]
  simp only [avgRisk_const_right', le_iInf_iff]
  refine fun Î¼ hÎ¼ â†¦ (iInf_le_lintegral (Î¼ := Î¼) _).trans_eq ?_
  rw [lintegral_lintegral_swap]
  Â· congr with Î¸
    rw [lintegral_mul_const _ (by fun_prop), mul_comm]
  Â· have := P.measurable_coe .univ
    fun_prop

lemma bayesRisk_of_subsingleton [IsMarkovKernel P] [SFinite Ï€] (hl : Measurable (uncurry â„“)) :
    bayesRisk â„“ P Ï€ = â¨… y, âˆ«â» Î¸, â„“ Î¸ y âˆ‚Ï€ := by
  simp [bayesRisk_of_subsingleton' hl]

end Subsingleton

section Compositions

/-- **Data processing inequality** for the Bayes risk with respect to a prior: composition of the
data generating kernel by a Markov kernel increases the risk. -/
lemma bayesRisk_le_bayesRisk_comp (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§)
    (Ï€ : Measure Î˜) (Î· : Kernel ğ“§ ğ“§') [IsMarkovKernel Î·] :
    bayesRisk â„“ P Ï€ â‰¤ bayesRisk â„“ (Î· âˆ˜â‚– P) Ï€ := by
  simp only [bayesRisk, avgRisk, le_iInf_iff]
  intro Îº hÎº
  rw [â† Îº.comp_assoc Î·]
  exact iInf_le_of_le (Îº âˆ˜â‚– Î·) (iInf_le_of_le inferInstance le_rfl)

/-- **Data processing inequality** for the Bayes risk with respect to a prior: taking the map of
the data generating kernel by a function increases the risk. -/
lemma bayesRisk_le_bayesRisk_map (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§)
    (Ï€ : Measure Î˜) {f : ğ“§ â†’ ğ“§'} (hf : Measurable f) :
    bayesRisk â„“ P Ï€ â‰¤ bayesRisk â„“ (P.map f) Ï€ := by
  rw [â† Kernel.deterministic_comp_eq_map hf]
  exact bayesRisk_le_bayesRisk_comp _ _ _ _

lemma bayesRisk_compProd_le_bayesRisk (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§)
    [IsSFiniteKernel P] (Ï€ : Measure Î˜) (Î· : Kernel (Î˜ Ã— ğ“§) ğ“§') [IsMarkovKernel Î·] :
    bayesRisk â„“ (P âŠ—â‚– Î·) Ï€ â‰¤ bayesRisk â„“ P Ï€ := by
  have : P = (Kernel.deterministic Prod.fst (by fun_prop)) âˆ˜â‚– (P âŠ—â‚– Î·) := by
    rw [Kernel.deterministic_comp_eq_map, â† Kernel.fst_eq, Kernel.fst_compProd]
  nth_rw 2 [this]
  exact bayesRisk_le_bayesRisk_comp _ _ _ _

end Compositions

end ProbabilityTheory
