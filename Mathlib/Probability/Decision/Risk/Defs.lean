/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Lorenzo Luccioli
-/

import Mathlib.Probability.Kernel.Composition.Comp

/-!
# Risk of an estimator

An estimation problem is defined by a parameter space `Î˜`, a data generating kernel `P : Kernel Î˜ ğ“§`
and a loss function `â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ`.
A (randomized) estimator is a kernel `Îº : Kernel ğ“§ ğ“¨` that maps data to estimates of a quantity
of interest that depends on the parameter. Often the quantity of interest is the parameter itself
and `ğ“¨ = Î˜`.
The quality of an estimate `y` when data comes from the distribution with parameter `Î¸` is measured
by the value of the loss function `â„“ Î¸ y`.

## Main definitions

The risk is the average loss of the estimator `Îº` on data generated by `P` with parameter `Î¸`,
equal to `âˆ«â» y, â„“ Î¸ y âˆ‚((Îº âˆ˜â‚– P) Î¸)`. We don't introduce a new definition for that risk.

* `bayesianRisk â„“ P Îº Ï€`: average of the risk with respect to the prior `Ï€ : Measure Î˜`.
* `bayesRiskPrior â„“ P Ï€`: minimum of the Bayesian risks over all estimators, that is over all
  Markov kernels `Îº : Kernel ğ“§ ğ“¨`.
* `bayesRisk â„“ P`: supremum of the Bayes risks over all priors (probability measures on `Î˜`).
* `minimaxRisk â„“ P`: infimum over all estimators of the maximum over `Î¸` of the risk.

-/

open MeasureTheory
open scoped ENNReal

namespace ProbabilityTheory

variable {Î˜ ğ“§ ğ“¨ : Type*} {mÎ˜ : MeasurableSpace Î˜} {mğ“§ : MeasurableSpace ğ“§} {mğ“¨ : MeasurableSpace ğ“¨}
  {â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ} {P : Kernel Î˜ ğ“§} {Îº : Kernel ğ“§ ğ“¨} {Ï€ : Measure Î˜}

/-- The bayesian risk of an estimator `Îº` on an estimation task with loss `â„“` and
data generating kernel `P` with respect to a prior `Ï€`. -/
noncomputable
def bayesianRisk (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) (Îº : Kernel ğ“§ ğ“¨) (Ï€ : Measure Î˜) : â„â‰¥0âˆ :=
  âˆ«â» Î¸, âˆ«â» y, â„“ Î¸ y âˆ‚((Îº âˆ˜â‚– P) Î¸) âˆ‚Ï€

/-- The Bayes risk with respect to a prior `Ï€`, defined as the infimum of the Bayesian risks of all
estimators. -/
noncomputable
def bayesRiskPrior {ğ“¨ : Type*} [MeasurableSpace ğ“¨]
    (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) (Ï€ : Measure Î˜) : â„â‰¥0âˆ :=
  â¨… (Îº : Kernel ğ“§ ğ“¨) (_ : IsMarkovKernel Îº), bayesianRisk â„“ P Îº Ï€

/-- The Bayes risk, defined as the supremum over priors of the Bayes risk with respect to
the prior. -/
noncomputable
def bayesRisk {ğ“¨ : Type*} [MeasurableSpace ğ“¨] (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) : â„â‰¥0âˆ :=
  â¨† (Ï€ : Measure Î˜) (_ : IsProbabilityMeasure Ï€), bayesRiskPrior â„“ P Ï€

/-- The minimax risk, defined as the infimum over estimators of the maximal risk of
the estimator. -/
noncomputable
def minimaxRisk {ğ“¨ : Type*} [MeasurableSpace ğ“¨] (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) : â„â‰¥0âˆ :=
  â¨… (Îº : Kernel ğ“§ ğ“¨) (_ : IsMarkovKernel Îº), â¨† Î¸, âˆ«â» y, â„“ Î¸ y âˆ‚((Îº âˆ˜â‚– P) Î¸)

section Zero

@[simp]
lemma bayesianRisk_zero_left (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (Îº : Kernel ğ“§ ğ“¨) (Ï€ : Measure Î˜) :
    bayesianRisk â„“ (0 : Kernel Î˜ ğ“§) Îº Ï€ = 0 := by simp [bayesianRisk]

@[simp]
lemma bayesianRisk_zero_right (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) (Ï€ : Measure Î˜) :
    bayesianRisk â„“ P (0 : Kernel ğ“§ ğ“¨) Ï€ = 0 := by simp [bayesianRisk]

@[simp]
lemma bayesianRisk_zero_prior (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) (Îº : Kernel ğ“§ ğ“¨) :
    bayesianRisk â„“ P Îº 0 = 0 := by simp [bayesianRisk]

@[simp]
lemma bayesRiskPrior_zero_left (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (Ï€ : Measure Î˜) [Nonempty ğ“¨] :
    bayesRiskPrior â„“ (0 : Kernel Î˜ ğ“§) Ï€ = 0 := by simp [bayesRiskPrior, iInf_subtype']

@[simp]
lemma bayesRiskPrior_zero_right (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) (P : Kernel Î˜ ğ“§) [Nonempty ğ“¨] :
    bayesRiskPrior â„“ P (0 : Measure Î˜) = 0 := by simp [bayesRiskPrior, iInf_subtype']

@[simp]
lemma bayesRisk_zero (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) [Nonempty ğ“¨] :
    bayesRisk â„“ (0 : Kernel Î˜ ğ“§) = 0 := by simp [bayesRisk]

@[simp]
lemma minimaxRisk_zero (â„“ : Î˜ â†’ ğ“¨ â†’ â„â‰¥0âˆ) [Nonempty ğ“¨] :
    minimaxRisk â„“ (0 : Kernel Î˜ ğ“§) = 0 := by simp [minimaxRisk, iInf_subtype']

end Zero

section Empty

@[simp]
lemma bayesianRisk_of_isEmpty [IsEmpty Î˜] : bayesianRisk â„“ P Îº Ï€ = 0 := by simp [bayesianRisk]

@[simp]
lemma bayesianRisk_of_isEmpty' [IsEmpty ğ“§] : bayesianRisk â„“ P Îº Ï€ = 0 := by
  simp [Subsingleton.elim P 0]

@[simp]
lemma bayesianRisk_of_isEmpty'' [IsEmpty ğ“¨] : bayesianRisk â„“ P Îº Ï€ = 0 := by
  simp [Subsingleton.elim Îº 0]

@[simp]
lemma bayesRiskPrior_of_isEmpty [IsEmpty ğ“§] : bayesRiskPrior â„“ P Ï€ = 0 := by
  simp [bayesRiskPrior]

@[simp]
lemma bayesRiskPrior_of_nonempty_of_isEmpty [Nonempty ğ“§] [IsEmpty ğ“¨] :
    bayesRiskPrior â„“ P Ï€ = âˆ := by
  have : IsEmpty (Subtype (@IsMarkovKernel ğ“§ ğ“¨ mğ“§ mğ“¨)) := by
    simp only [isEmpty_subtype]
    intro Îº
    rw [Subsingleton.elim Îº 0]
    exact Kernel.not_isMarkovKernel_zero
  simp [bayesRiskPrior, iInf_subtype']

@[simp]
lemma bayesRisk_of_isEmpty [IsEmpty ğ“§] : bayesRisk â„“ P = 0 := by
  simp [bayesRisk]

@[simp]
lemma minimaxRisk_of_isEmpty [IsEmpty ğ“§] : minimaxRisk â„“ P = 0 := by
  simp [minimaxRisk, Subsingleton.elim P 0]

end Empty

end ProbabilityTheory
