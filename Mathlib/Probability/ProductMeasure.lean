/-
Copyright (c) 2025 Etienne Marion. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Etienne Marion
-/
import Mathlib.Probability.Kernel.Composition.MeasureComp
import Mathlib.Probability.Kernel.IonescuTulcea.Traj

/-!
# Infinite product of probability measures

This files provides a definition for the product measure of an arbitrary family of probability
measures. Given `Œº : (i : Œπ) ‚Üí Measure (X i)`, `productMeasure Œº` is the only probability measure
 `ŒΩ` over `Œ† i, X i` such that `ŒΩ (Set.pi s t) = ‚àè i ‚àà s, Œº i (t i)`, with `s : Finset Œπ` and
such that `‚àÄ i ‚àà s, MeasurableSet (t i)` (see `eq_productMeasure` and `productMeasure_boxes`).
We also provide a few results regarding integration against this measure.

## Main definition

* `productMeasure Œº`: The product measure of the family of probability measures `Œº`.

## Main statements

* `eq_productMeasure`: Any measure which gives to a finite product of sets the mass which is the
  product of their measures is the product measure.

* `productMeasure_boxes`: the product measure gives to finite products of sets a mass which is
  the product of their masses.

* `productMeasure_cylinder`: `productMeasure Œº (cylinder s S) = Measure.pi (fun i : s ‚Ü¶ Œº i) S`

## Implementation notes

To construct the product measure we first use the kernel `traj` obtained via the Ionescu-Tulcea
theorem to construct the measure over a product indexed by `‚Ñï`, which is `infinitePiNat`. This
is an implementation detail and should not be used directly. Then we construct the product measure
over an arbitrary type by extending `piContent Œº` thanks to Carath√©odory's theorem. The key lemma
to do so is `piContent_tendsto_zero`, which states that `piContent Œº (A n)` tends to zero if
`‚ãÇ n, A n = ‚àÖ`. We prove this lemma by reducing to the case of an at most countable product,
in which case `piContent Œº` is known to be a true measure (see `piContent_eq_measure_pi` and
piContent_eq_infinitePiNat).

## Tags

infinite product measure
-/

open MeasureTheory ProbabilityTheory Finset Filter Topology Preorder MeasurableEquiv

open scoped ENNReal

namespace MeasureTheory

section Preliminaries

variable {Œπ : Type*} {Œ± : Œπ ‚Üí Type*}

lemma _root_.preimage_restrict‚ÇÇ {I J : Finset Œπ} [‚àÄ i : Œπ, Decidable (i ‚àà I)]
    (hIJ : I ‚äÜ J) (s : (i : I) ‚Üí Set (Œ± i)) :
    (restrict‚ÇÇ hIJ) ‚Åª¬π' (Set.univ.pi s) =
      (@Set.univ J).pi (fun j ‚Ü¶ if h : j.1 ‚àà I then s ‚ü®j.1, h‚ü© else Set.univ) := by
  ext x
  simp only [Set.mem_preimage, Set.mem_pi, Set.mem_univ, true_implies, Subtype.forall]
  refine ‚ü®fun h i hi ‚Ü¶ ?_, fun h i i_mem ‚Ü¶ by simpa [i_mem] using h i (hIJ i_mem)‚ü©
  split_ifs with i_mem
  ¬∑ exact h i i_mem
  ¬∑ trivial

lemma _root_.preimage_restrict {I : Finset Œπ} [‚àÄ i : Œπ, Decidable (i ‚àà I)]
    (s : (i : I) ‚Üí Set (Œ± i)) :
    I.restrict ‚Åª¬π' (Set.univ.pi s) =
      Set.pi I (fun i ‚Ü¶ if h : i ‚àà I then s ‚ü®i, h‚ü© else Set.univ) := by
  ext x
  simp only [Set.mem_preimage, Set.mem_pi, Set.mem_univ, restrict, forall_const, Subtype.forall,
    mem_coe]
  refine ‚ü®fun h i hi ‚Ü¶ by simpa [hi] using h i hi, fun h i hi ‚Ü¶ ?_‚ü©
  convert h i hi
  rw [dif_pos hi]

variable {X : Œπ ‚Üí Type*} [‚àÄ i, MeasurableSpace (X i)]
variable (Œº : (i : Œπ) ‚Üí Measure (X i)) [hŒº : ‚àÄ i, IsProbabilityMeasure (Œº i)]

/-- Consider a family of probability measures. You can take their products for any finite
subfamily. This gives a projective family of measures, see `IsProjectiveMeasureFamily`. -/
lemma isProjectiveMeasureFamily_pi :
    IsProjectiveMeasureFamily (fun I : Finset Œπ ‚Ü¶ (Measure.pi (fun i : I ‚Ü¶ Œº i))) := by
  refine fun I J hJI ‚Ü¶ Measure.pi_eq (fun s ms ‚Ü¶ ?_)
  classical
  rw [Measure.map_apply (measurable_restrict‚ÇÇ hJI) (.univ_pi ms),
    preimage_restrict‚ÇÇ hJI, Measure.pi_pi]
  let g := fun i ‚Ü¶ (Œº i) (if hi : i ‚àà J then s ‚ü®i, hi‚ü© else Set.univ)
  conv_lhs => change ‚àè i : I, g i
  have h2 : univ.prod (fun i : J ‚Ü¶ (Œº i) (s i)) = univ.prod (fun i : J ‚Ü¶ g i) :=
    Finset.prod_congr rfl (by simp [g])
  rw [h2, prod_coe_sort, prod_coe_sort, prod_subset hJI (fun _ h h' ‚Ü¶ by simp [g, h, h'])]

/-- Consider a family of probability measures. You can take their products for any finite
subfamily. This gives an additive content on the measurable cylinders. -/
noncomputable def piContent : AddContent (measurableCylinders X) :=
  projectiveFamilyContent (isProjectiveMeasureFamily_pi Œº)

lemma piContent_cylinder {I : Finset Œπ} {S : Set (Œ† i : I, X i)} (hS : MeasurableSet S) :
    piContent Œº (cylinder I S) = Measure.pi (fun i : I ‚Ü¶ Œº i) S :=
  projectiveFamilyContent_cylinder _ hS

theorem piContent_eq_measure_pi [Fintype Œπ] {s : Set (Œ† i, X i)} (hs : MeasurableSet s) :
    piContent Œº s = Measure.pi Œº s := by
  let aux : (Œ† i : univ, X i) ‚Üí (Œ† i, X i) := fun x i ‚Ü¶ x ‚ü®i, mem_univ i‚ü©
  have maux : Measurable aux := measurable_pi_lambda _ (fun _ ‚Ü¶ measurable_pi_apply _)
  have pi_eq : Measure.pi Œº = (Measure.pi (fun i : univ ‚Ü¶ Œº i)).map aux := by
    refine Measure.pi_eq fun a ma ‚Ü¶ ?_
    rw [Measure.map_apply maux (.univ_pi ma)]
    have : aux ‚Åª¬π' Set.univ.pi a = Set.univ.pi (fun i : @univ Œπ _ ‚Ü¶ a i) := by ext x; simp [aux]
    rw [this, Measure.pi_pi]
    simp
  have : s = cylinder univ (aux ‚Åª¬π' s) := by ext x; simp [aux]
  nth_rw 1 [this]
  rw [piContent_cylinder _ (maux hs), pi_eq, Measure.map_apply maux hs]

end Preliminaries

section Nat

open Kernel

/-! ### Product of measures indexed by `‚Ñï` -/

variable {X : ‚Ñï ‚Üí Type*}

lemma _root_.IocProdIoc_preim {a b c : ‚Ñï} (hab : a ‚â§ b) (hbc : b ‚â§ c)
    (s : (i : Ioc a c) ‚Üí Set (X i)) :
    IocProdIoc a b c ‚Åª¬π' (Set.univ.pi s) =
      (Set.univ.pi <| restrict‚ÇÇ (œÄ := (fun n ‚Ü¶ Set (X n))) (Ioc_subset_Ioc_right hbc) s) √óÀ¢
        (Set.univ.pi <| restrict‚ÇÇ (œÄ := (fun n ‚Ü¶ Set (X n))) (Ioc_subset_Ioc_left hab) s) := by
  ext x
  simp only [Set.mem_preimage, Set.mem_pi, Set.mem_univ, IocProdIoc, forall_const, Subtype.forall,
    mem_Ioc, Set.mem_prod, restrict‚ÇÇ]
  refine ‚ü®fun h ‚Ü¶ ‚ü®fun i ‚ü®hi1, hi2‚ü© ‚Ü¶ ?_, fun i ‚ü®hi1, hi2‚ü© ‚Ü¶ ?_‚ü©, fun ‚ü®h1, h2‚ü© i ‚ü®hi1, hi2‚ü© ‚Ü¶ ?_‚ü©
  ¬∑ convert h i ‚ü®hi1, hi2.trans hbc‚ü©
    rw [dif_pos hi2]
  ¬∑ convert h i ‚ü®lt_of_le_of_lt hab hi1, hi2‚ü©
    rw [dif_neg (not_le.2 hi1)]
  ¬∑ split_ifs with hi3
    ¬∑ exact h1 i ‚ü®hi1, hi3‚ü©
    ¬∑ exact h2 i ‚ü®not_le.1 hi3, hi2‚ü©

lemma _root_.IicProdIoc_preim {a b : ‚Ñï} (hab : a ‚â§ b) (s : (i : Iic b) ‚Üí Set (X i)) :
    IicProdIoc a b ‚Åª¬π' (Set.univ.pi s) =
      (Set.univ.pi <| frestrictLe‚ÇÇ (œÄ := (fun n ‚Ü¶ Set (X n))) hab s) √óÀ¢
        (Set.univ.pi <| restrict‚ÇÇ (œÄ := (fun n ‚Ü¶ Set (X n))) Ioc_subset_Iic_self s) := by
  ext x
  simp only [Set.mem_preimage, Set.mem_pi, Set.mem_univ, IicProdIoc_def, forall_const,
    Subtype.forall, mem_Iic, Set.mem_prod, frestrictLe‚ÇÇ_apply, restrict‚ÇÇ, mem_Ioc]
  refine ‚ü®fun h ‚Ü¶ ‚ü®fun i hi ‚Ü¶ ?_, fun i ‚ü®hi1, hi2‚ü© ‚Ü¶ ?_‚ü©, fun ‚ü®h1, h2‚ü© i hi ‚Ü¶ ?_‚ü©
  ¬∑ convert h i (hi.trans hab)
    rw [dif_pos hi]
  ¬∑ convert h i hi2
    rw [dif_neg (not_le.2 hi1)]
  ¬∑ split_ifs with hi3
    ¬∑ exact h1 i hi3
    ¬∑ exact h2 i ‚ü®not_le.1 hi3, hi‚ü©

variable [‚àÄ n, MeasurableSpace (X n)]
  (Œº : (n : ‚Ñï) ‚Üí Measure (X n)) [hŒº : ‚àÄ n, IsProbabilityMeasure (Œº n)]

/-- Infinite product measure indexed by `‚Ñï`. This is an auxiliary construction, you should use
the generic product measure `Measure.productMeasure`. -/
noncomputable def Measure.infinitePiNat : Measure ((n : ‚Ñï) ‚Üí X n) :=
  (traj (fun n ‚Ü¶ const _ (Œº (n + 1))) 0) ‚àò‚Çò (Measure.pi (fun i : Iic 0 ‚Ü¶ Œº i))

instance : IsProbabilityMeasure (Measure.infinitePiNat Œº) := by
  rw [Measure.infinitePiNat]; infer_instance

lemma Measure.pi_prod_map_IocProdIoc {a b c : ‚Ñï} (hab : a ‚â§ b) (hbc : b ‚â§ c) :
    ((Measure.pi (fun i : Ioc a b ‚Ü¶ Œº i)).prod (Measure.pi (fun i : Ioc b c ‚Ü¶ Œº i))).map
      (IocProdIoc a b c) = Measure.pi (fun i : Ioc a c ‚Ü¶ Œº i) := by
  refine (Measure.pi_eq fun s ms ‚Ü¶ ?_).symm
  simp_rw [Measure.map_apply measurable_IocProdIoc (.univ_pi ms), IocProdIoc_preim hab hbc,
    Measure.prod_prod, Measure.pi_pi, prod_eq_prod_extend]
  nth_rw 1 [Eq.comm, ‚Üê Ioc_union_Ioc_eq_Ioc hab hbc, prod_union (Ioc_disjoint_Ioc a b c)]
  congr 1 <;> refine prod_congr rfl fun x hx ‚Ü¶ ?_
  ¬∑ rw [Function.extend_val_apply x hx, Function.extend_val_apply x (Ioc_subset_Ioc_right hbc hx),
    restrict‚ÇÇ]
  ¬∑ rw [Function.extend_val_apply x hx, Function.extend_val_apply x (Ioc_subset_Ioc_left hab hx),
    restrict‚ÇÇ]

lemma Measure.pi_prod_map_IicProdIoc {a b : ‚Ñï} :
    ((Measure.pi (fun i : Iic a ‚Ü¶ Œº i)).prod (Measure.pi (fun i : Ioc a b ‚Ü¶ Œº i))).map
      (IicProdIoc a b) = Measure.pi (fun i : Iic b ‚Ü¶ Œº i) := by
  obtain hab | hba := le_total a b
  ¬∑ refine (Measure.pi_eq fun s ms ‚Ü¶ ?_).symm
    simp_rw [Measure.map_apply measurable_IicProdIoc (.univ_pi ms), IicProdIoc_preim hab,
      Measure.prod_prod, Measure.pi_pi, prod_eq_prod_extend]
    nth_rw 1 [Eq.comm, ‚Üê Iic_union_Ioc_eq_Iic hab, prod_union (Iic_disjoint_Ioc le_rfl)]
    congr 1 <;> refine prod_congr rfl fun x hx ‚Ü¶ ?_
    ¬∑ rw [Function.extend_val_apply x hx, Function.extend_val_apply x (Iic_subset_Iic.2 hab hx),
      frestrictLe‚ÇÇ, restrict‚ÇÇ]
    ¬∑ rw [Function.extend_val_apply x hx, Function.extend_val_apply x (Ioc_subset_Iic_self hx),
      restrict‚ÇÇ]
  ¬∑ rw [IicProdIoc_le hba, ‚Üê Measure.map_map, ‚Üê Measure.fst, Measure.fst_prod]
    ¬∑ exact isProjectiveMeasureFamily_pi Œº (Iic a) (Iic b) (Iic_subset_Iic.2 hba) |>.symm
    all_goals fun_prop

lemma Measure.map_piSingleton (Œº : (n : ‚Ñï) ‚Üí Measure (X n)) [‚àÄ n, SigmaFinite (Œº n)] (n : ‚Ñï) :
    (Œº (n + 1)).map (piSingleton n) = Measure.pi (fun i : Ioc n (n + 1) ‚Ü¶ Œº i) := by
  refine (Measure.pi_eq fun s hs ‚Ü¶ ?_).symm
  have : Subsingleton (Ioc n (n + 1)) := by
    rw [Nat.Ioc_succ_singleton]
    infer_instance
  rw [Fintype.prod_subsingleton _ ‚ü®n + 1, mem_Ioc.2 (by omega)‚ü©, Measure.map_apply]
  ¬∑ congr with x
    simp only [Set.mem_preimage, Set.mem_pi, Set.mem_univ, forall_const, Subtype.forall,
      Nat.Ioc_succ_singleton, mem_singleton]
    exact ‚ü®fun h ‚Ü¶ h (n + 1) rfl, fun h a b ‚Ü¶ b.symm ‚ñ∏ h‚ü©
  ¬∑ exact (piSingleton n).measurable
  ¬∑ exact .univ_pi hs

theorem partialTraj_const_restrict‚ÇÇ {a b : ‚Ñï} :
    (partialTraj (fun n ‚Ü¶ const _ (Œº (n + 1))) a b).map (restrict‚ÇÇ (Ioc_subset_Iic_self (a := a))) =
    const _ (Measure.pi (fun i : Ioc a b ‚Ü¶ Œº i)) := by
  obtain hab | hba := lt_or_le a b
  ¬∑ refine Nat.le_induction ?_ (fun n hn hind ‚Ü¶ ?_) b (Nat.succ_le.2 hab) <;> ext1 x‚ÇÄ
    ¬∑ rw [partialTraj_succ_self, ‚Üê Kernel.map_comp_right, map_apply, prod_apply, map_apply,
        const_apply, const_apply, Measure.map_piSingleton, restrict‚ÇÇ_comp_IicProdIoc,
        Measure.map_snd_prod, measure_univ, one_smul]
      all_goals fun_prop
    ¬∑ have : (restrict‚ÇÇ (Ioc_subset_Iic_self (a := a))) ‚àò (IicProdIoc (X := X) n (n + 1)) =
          (IocProdIoc a n (n + 1)) ‚àò (Prod.map (restrict‚ÇÇ Ioc_subset_Iic_self) id) := rfl
      rw [const_apply, partialTraj_succ_of_le (by omega), map_const, prod_const_comp, id_comp,
        Kernel.map_map, this, ‚Üê Kernel.map_map, Kernel.map_prod, hind,  Kernel.map_id, map_apply,
        prod_apply, const_apply, const_apply, Measure.map_piSingleton,
        Measure.pi_prod_map_IocProdIoc]
      any_goals fun_prop
      all_goals omega
  ¬∑ have : IsEmpty (Ioc a b) := by simpa [hba] using Subtype.isEmpty_false
    ext x s ms
    by_cases hs : s.Nonempty
    ¬∑ rw [Subsingleton.eq_univ_of_nonempty hs, @measure_univ .., measure_univ]
      exact (IsMarkovKernel.map _ (measurable_restrict‚ÇÇ _)) |>.isProbabilityMeasure x
    ¬∑ rw [Set.not_nonempty_iff_eq_empty.1 hs]
      simp

theorem partialTraj_const {a b : ‚Ñï} :
    partialTraj (fun n ‚Ü¶ const _ (Œº (n + 1))) a b =
      (Kernel.id √ó‚Çñ (const _ (Measure.pi (fun i : Ioc a b ‚Ü¶ Œº i)))).map (IicProdIoc a b) := by
  rw [partialTraj_eq_prod, partialTraj_const_restrict‚ÇÇ]

theorem isProjectiveLimit_infinitePiNat :
    IsProjectiveLimit (Measure.infinitePiNat Œº)
      (fun I : Finset ‚Ñï ‚Ü¶ (Measure.pi (fun i : I ‚Ü¶ Œº i))) := by
  have _ := ProbabilityMeasure.nonempty ‚ü®Œº 0, hŒº 0‚ü©
  intro I
  simp_rw [isProjectiveMeasureFamily_pi Œº _ _ I.sub_Iic]
  rw [‚Üê restrict‚ÇÇ_comp_restrict I.sub_Iic, ‚Üê Measure.map_map, ‚Üê frestrictLe, Measure.infinitePiNat,
    Measure.map_comp, traj_map_frestrictLe, partialTraj_const, ‚Üê Measure.map_comp,
    ‚Üê Measure.compProd_eq_comp_prod, Measure.compProd_const, Measure.pi_prod_map_IicProdIoc]
  any_goals fun_prop

lemma infinitePiNat_map_restrict {I : Finset ‚Ñï} :
    (Measure.infinitePiNat Œº).map I.restrict = Measure.pi fun i : I ‚Ü¶ Œº i :=
  isProjectiveLimit_infinitePiNat Œº I

theorem piContent_eq_infinitePiNat {A : Set ((n : ‚Ñï) ‚Üí X n)} (hA : A ‚àà measurableCylinders X) :
    piContent Œº A = Measure.infinitePiNat Œº A := by
  obtain ‚ü®s, S, mS, rfl‚ü© : ‚àÉ s S, MeasurableSet S ‚àß A = cylinder s S := by
    simpa [mem_measurableCylinders] using hA
  rw [piContent_cylinder _ mS, cylinder, ‚Üê Measure.map_apply (measurable_restrict _) mS,
    isProjectiveLimit_infinitePiNat Œº]

end Nat

section ProductMeasure

open Measure

/-! ### Product of infinitely many probability measures -/

variable {Œπ : Type*} {X : Œπ ‚Üí Type*} [‚àÄ i, MeasurableSpace (X i)]
  (Œº : (i : Œπ) ‚Üí Measure (X i)) [hŒº : ‚àÄ i, IsProbabilityMeasure (Œº i)]

lemma infinitePiNat_map_piCongrLeft (e : ‚Ñï ‚âÉ Œπ) {s : Set (Œ† i, X i)}
    (hs : s ‚àà measurableCylinders X) :
    (infinitePiNat (fun n ‚Ü¶ Œº (e n))).map (piCongrLeft X e) s = piContent Œº s := by
  obtain ‚ü®I, S, hS, rfl‚ü© := (mem_measurableCylinders s).1 hs
  rw [map_apply _ hS.cylinder, cylinder, ‚Üê Set.preimage_comp, coe_piCongrLeft,
    restrict_comp_piCongrLeft, Set.preimage_comp, ‚Üê map_apply,
    infinitePiNat_map_restrict (fun n ‚Ü¶ Œº (e n)), ‚Üê cylinder, piContent_cylinder Œº hS,
    ‚Üê pi_map_piCongrLeft (e.frestrict I), map_apply _ hS]; rfl
  any_goals fun_prop
  exact hS.preimage (by fun_prop)

/-- This is the key theorem to build the product of an arbitrary family of probability measures:
the `piContent` of a decreasing sequence of cylinders with empty intersection converges to `0`.

This implies the `œÉ`-additivity of `piContent` (see `addContent_iUnion_eq_sum_of_tendsto_zero`),
which allows to extend it to the `œÉ`-algebra by Carath√©odory's theorem. -/
theorem piContent_tendsto_zero {A : ‚Ñï ‚Üí Set (Œ† i, X i)} (A_mem : ‚àÄ n, A n ‚àà measurableCylinders X)
    (A_anti : Antitone A) (A_inter : ‚ãÇ n, A n = ‚àÖ) :
    Tendsto (fun n ‚Ü¶ piContent Œº (A n)) atTop (ùìù 0) := by
  have : ‚àÄ i, Nonempty (X i) := fun i ‚Ü¶ ProbabilityMeasure.nonempty ‚ü®Œº i, hŒº i‚ü©
  have A_cyl n : ‚àÉ s S, MeasurableSet S ‚àß A n = cylinder s S :=
    (mem_measurableCylinders _).1 (A_mem n)
  choose s S mS A_eq using A_cyl
  -- The family `(A‚Çô)` only depends on a countable set of coordinates, called `u`. Therefore our
  -- goal is to see it as a family indexed by this countable set, because on the product indexed
  -- by this countable set we can build a measure. To do so we have to pull back our cylinders
  -- along the injection from `Œ† i : u, X i` to `Œ† i, X i`.
  let u := ‚ãÉ n, (s n).toSet
  let ŒΩ := fun i : u ‚Ü¶ Œº i
  -- `t‚Çô` will be `s‚Çô` seen as a subset of `u`.
  let t n : Finset u := (s n).preimage Subtype.val Subtype.val_injective.injOn
  classical
  -- The map `f` allows to pull back `A‚Çô`
  let f : (Œ† i : u, X i) ‚Üí Œ† i, X i :=
    fun x i ‚Ü¶ if hi : i ‚àà u then x ‚ü®i, hi‚ü© else Classical.ofNonempty
  -- `aux` is the obvious equivalence between `s‚Çô` and `t‚Çô`
  let aux n : t n ‚âÉ s n :=
    { toFun := fun i ‚Ü¶ ‚ü®i.1.1, mem_preimage.1 i.2‚ü©
      invFun := fun i ‚Ü¶ ‚ü®‚ü®i.1, Set.mem_iUnion.2 ‚ü®n, i.2‚ü©‚ü©, mem_preimage.2 i.2‚ü©
      left_inv := fun i ‚Ü¶ by simp
      right_inv := fun i ‚Ü¶ by simp }
  -- Finally `g‚Çô` is the equivalence between the product indexed by `t‚Çô` and the one indexed by `s‚Çô`
  let g n := (aux n).piCongrLeft (fun i : s n ‚Ü¶ X i)
  -- Mapping from the product indexed by `u` by `f` and then restricting to `s‚Çô` is the same as
  -- first restricting to `t‚Çô` and then mapping by `g‚Çô`
  have r_comp_f n : (s n).restrict ‚àò f = (g n) ‚àò (fun (x : Œ† i : u, X i) i ‚Ü¶ x i) := by
    ext x i
    simp only [Function.comp_apply, Finset.restrict, subset_refl, Set.coe_inclusion,
      Equiv.piCongrLeft_apply, Equiv.coe_fn_symm_mk, f, aux, g, t]
    rw [dif_pos (Set.mem_iUnion.2 ‚ü®n, i.2‚ü©)]
  -- `B‚Çô` is the same as `A‚Çô` but in the product indexed by `u`
  let B n := f ‚Åª¬π' (A n)
  -- `T‚Çô` is the same as `S‚Çô` but in the product indexed by `u`
  let T n := (g n) ‚Åª¬π' (S n)
  -- We now tranfer the properties of `A‚Çô` and `S‚Çô` to `B‚Çô` and `T‚Çô`
  have B_eq n : B n = cylinder (t n) (T n) := by
    simp_rw [B, A_eq, cylinder, ‚Üê Set.preimage_comp, r_comp_f]; rfl
  have mg n : Measurable (g n) := by fun_prop
  have mT n : MeasurableSet (T n) := (mS n).preimage (mg n)
  have B_mem n : B n ‚àà measurableCylinders (fun i : u ‚Ü¶ X i) :=
    (mem_measurableCylinders (B n)).2 ‚ü®t n, T n, mT n, B_eq n‚ü©
  have mB n : MeasurableSet (B n) := .of_mem_measurableCylinders (B_mem n)
  have B_anti : Antitone B := fun m n hmn ‚Ü¶ Set.preimage_mono <| A_anti hmn
  have B_inter : ‚ãÇ n, B n = ‚àÖ := by
    simp_rw [B, ‚Üê Set.preimage_iInter, A_inter, Set.preimage_empty]
  -- We now rewrite `piContent Œº (A n)` as `piContent (fun i : u ‚Ü¶ Œº i) (B n)`. Then there are two
  -- cases: either `u` is finite and we rewrite it to the finite product measure, either
  -- it is countable and we rewrite it to the pushforward measure of `infinitePiNat`. In both cases
  -- we have an actual measure and we can conclude with `tendsto_measure_iInter_atTop`.
  conv =>
    enter [1]; ext n
    rw [A_eq, piContent_cylinder Œº (mS n), ‚Üê pi_map_piCongrLeft (aux n),
      map_apply (by fun_prop) (mS n)]
    change (Measure.pi (fun i : t n ‚Ü¶ Œº i)) (T n)
    rw [‚Üê piContent_cylinder (fun i : u ‚Ü¶ Œº i) (mT n), ‚Üê B_eq n]
  obtain u_fin | u_inf := finite_or_infinite u
  ¬∑ let _ := Fintype.ofFinite u
    simp_rw [fun n ‚Ü¶ piContent_eq_measure_pi (fun i : u ‚Ü¶ Œº i) (mB n)]
    convert tendsto_measure_iInter_atTop (fun n ‚Ü¶ (mB n).nullMeasurableSet) B_anti
      ‚ü®0, measure_ne_top _ _‚ü©
    rw [B_inter, measure_empty]; infer_instance
  ¬∑ -- If `u` is infinite, then we have an equivalence with `‚Ñï` so we can apply `secondLemma`.
    have count_u : Countable u := Set.countable_iUnion (fun n ‚Ü¶ (s n).countable_toSet)
    obtain ‚ü®œÜ, -‚ü© := Classical.exists_true_of_nonempty (Œ± := ‚Ñï ‚âÉ u) nonempty_equiv_of_countable
    conv => enter [1]; ext n; rw [‚Üê infinitePiNat_map_piCongrLeft _ œÜ (B_mem n)]
    convert tendsto_measure_iInter_atTop (fun n ‚Ü¶ (mB n).nullMeasurableSet) B_anti
      ‚ü®0, measure_ne_top _ _‚ü©
    rw [B_inter, measure_empty]; infer_instance

/-- The `projectiveFamilyContent` associated to a family of probability measures is œÉ-subadditive. -/
theorem piContent_sigma_subadditive ‚¶Éf : ‚Ñï ‚Üí Set (Œ† i, X i)‚¶Ñ
    (hf : ‚àÄ n, f n ‚àà measurableCylinders X) (hf_Union : (‚ãÉ n, f n) ‚àà measurableCylinders X) :
    piContent Œº (‚ãÉ n, f n) ‚â§
    ‚àë' n, piContent Œº (f n) := by
  refine addContent_iUnion_le_of_addContent_iUnion_eq_tsum
    isSetRing_measurableCylinders (fun f hf hf_Union hf' ‚Ü¶ ?_) f hf hf_Union
  exact addContent_iUnion_eq_sum_of_tendsto_zero isSetRing_measurableCylinders
    (piContent Œº) (fun s hs ‚Ü¶ projectiveFamilyContent_ne_top _)
    (fun _ ‚Ü¶ piContent_tendsto_zero Œº) hf hf_Union hf'

/-- The product measure of an arbitrary family of probability measures. It is defined as the unique
extension of the function which gives to cylinders the measure given by the associated product
measure. -/
noncomputable def Measure.productMeasure : Measure (Œ† i, X i) :=
  (piContent Œº).measure isSetSemiring_measurableCylinders
    generateFrom_measurableCylinders.ge (piContent_sigma_subadditive Œº)

open Measure

/-- The product measure is the projective limit of the partial product measures. This ensures
uniqueness and expresses the value of the product measures applied to cylinders. -/
theorem isProjectiveLimit_productMeasure :
    IsProjectiveLimit (productMeasure Œº) (fun I : Finset Œπ ‚Ü¶ (Measure.pi (fun i : I ‚Ü¶ Œº i))) := by
  intro I
  ext1 s hs
  rw [map_apply (measurable_restrict I) hs, productMeasure, AddContent.measure_eq,
    ‚Üê cylinder, piContent_cylinder Œº hs]
  ¬∑ exact generateFrom_measurableCylinders.symm
  ¬∑ exact cylinder_mem_measurableCylinders _ _ hs

instance : IsProbabilityMeasure (productMeasure Œº) := by
  constructor
  rw [‚Üê cylinder_univ ‚àÖ, cylinder, ‚Üê map_apply (measurable_restrict _) .univ,
    isProjectiveLimit_productMeasure Œº, measure_univ]

theorem eq_productMeasure {ŒΩ : Measure (Œ† i, X i)}
    (hŒΩ : ‚àÄ s : Finset Œπ, ‚àÄ t : (i : Œπ) ‚Üí Set (X i),
      (‚àÄ i ‚àà s, MeasurableSet (t i)) ‚Üí ŒΩ (Set.pi s t) = ‚àè i ‚àà s, Œº i (t i)) :
    ŒΩ = productMeasure Œº := by
  refine (isProjectiveLimit_productMeasure Œº).unique ?_ |>.symm
  refine fun s ‚Ü¶ (pi_eq fun t ht ‚Ü¶ ?_).symm
  classical
  rw [Measure.map_apply, preimage_restrict, hŒΩ, ‚Üê prod_attach, univ_eq_attach]
  ¬∑ congr with i
    rw [dif_pos i.2]
  any_goals fun_prop
  ¬∑ exact fun i hi ‚Ü¶ dif_pos hi ‚ñ∏ ht ‚ü®i, hi‚ü©
  ¬∑ exact .univ_pi ht

-- TODO: add a version for an infinite product
lemma productMeasure_boxes {s : Finset Œπ} {t : (i : Œπ) ‚Üí Set (X i)}
    (mt : ‚àÄ i ‚àà s, MeasurableSet (t i)) :
    productMeasure Œº (Set.pi s t) = ‚àè i ‚àà s, (Œº i) (t i) := by
  have : Set.pi s t = cylinder s ((@Set.univ s).pi (fun i : s ‚Ü¶ t i)) := by
    ext x
    simp
  rw [this, cylinder, ‚Üê map_apply, isProjectiveLimit_productMeasure Œº, pi_pi]
  ¬∑ rw [Finset.univ_eq_attach, Finset.prod_attach _ (fun i ‚Ü¶ (Œº i) (t i))]
  ¬∑ exact measurable_restrict _
  ¬∑ exact .univ_pi fun i ‚Ü¶ mt i.1 i.2

theorem productMeasure_eq_pi [Fintype Œπ] : productMeasure Œº = Measure.pi Œº := by
  refine (pi_eq fun s hs ‚Ü¶ ?_).symm
  rw [‚Üê coe_univ, productMeasure_boxes]
  simpa

lemma productMeasure_cylinder {s : Finset Œπ} {S : Set (Œ† i : s, X i)} (mS : MeasurableSet S) :
    productMeasure Œº (cylinder s S) = Measure.pi (fun i : s ‚Ü¶ Œº i) S := by
  rw [cylinder, ‚Üê Measure.map_apply (measurable_restrict _) mS, isProjectiveLimit_productMeasure Œº]

theorem integral_dep_productMeasure {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
    {s : Finset Œπ} {f : (Œ† i : s, X i) ‚Üí E}
    (hf : AEStronglyMeasurable f (Measure.pi (fun i : s ‚Ü¶ Œº i))) :
    ‚à´ y, f (s.restrict y) ‚àÇproductMeasure Œº = ‚à´ y, f y ‚àÇMeasure.pi (fun i : s ‚Ü¶ Œº i) := by
  rw [‚Üê integral_map, isProjectiveLimit_productMeasure Œº]
  ¬∑ fun_prop
  ¬∑ rwa [isProjectiveLimit_productMeasure Œº]

/-- The canonical filtration on dependent functions indexed by `Œπ`, where `piFinset s` consists of
measurable sets depending only on coordinates is `s`. -/
def Filtration.piFinset : @Filtration (Œ† i, X i) (Finset Œπ) _ inferInstance where
  seq s := (inferInstance : MeasurableSpace (Œ† i : s, X i)).comap s.restrict
  mono' s t hst := by
    simp only
    rw [‚Üê restrict‚ÇÇ_comp_restrict hst, ‚Üê MeasurableSpace.comap_comp]
    exact MeasurableSpace.comap_mono (measurable_restrict‚ÇÇ _).comap_le
  le' s := (measurable_restrict s).comap_le

open Filtration

/-- If a function is strongly measurable with respect to the œÉ-algebra generated by
the finite set of coordinates `s`, then it only depends on those coordinates. -/
theorem dependsOn_of_stronglyMeasurable' {E : Type*} [NormedAddCommGroup E]
    {s : Finset Œπ} {f : (Œ† i, X i) ‚Üí E}
    (mf : StronglyMeasurable[piFinset s] f) : DependsOn f s :=
  dependsOn_iff_factorsThrough.2 (factorsThrough_of_stronglyMeasurable_comap mf)

theorem integral_stronglyMeasurable [DecidableEq Œπ] {E : Type*} [NormedAddCommGroup E]
    [NormedSpace ‚Ñù E] {s : Finset Œπ} {f : (Œ† i, X i) ‚Üí E}
    (mf : StronglyMeasurable[piFinset s] f) (x : Œ† i, X i) :
    ‚à´ y, f y ‚àÇproductMeasure Œº =
    ‚à´ y, f (Function.updateFinset x s y) ‚àÇMeasure.pi (fun i : s ‚Ü¶ Œº i) := by
  let g : (Œ† i : s, X i) ‚Üí E := fun y ‚Ü¶ f (Function.updateFinset x _ y)
  have this y : g (s.restrict y) = f y := by
    apply dependsOn_of_stronglyMeasurable' mf
    intro i hi
    simp only [Function.updateFinset, Finset.restrict, dite_eq_ite, ite_eq_left_iff]
    exact fun h ‚Ü¶ (h hi).elim
  rw [‚Üê integral_congr_ae <| Eventually.of_forall this, integral_dep_productMeasure]
  exact mf.comp_measurable (measurable_updateFinset.mono le_rfl (piFinset.le s))
    |>.aestronglyMeasurable

theorem lintegral_dep {s : Finset Œπ} {f : (Œ† i : s, X i) ‚Üí ‚Ñù‚â•0‚àû} (hf : Measurable f) :
    ‚à´‚Åª y, f (s.restrict y) ‚àÇproductMeasure Œº = ‚à´‚Åª y, f y ‚àÇMeasure.pi (fun i : s ‚Ü¶ Œº i) := by
  rw [‚Üê lintegral_map hf (measurable_restrict _), isProjectiveLimit_productMeasure Œº]

/-- If a function is measurable with respect to the œÉ-algebra generated by
the finite set of coordinates `s`, then it only depends on those coordinates. -/
theorem measurable_dependsOn' {s : Finset Œπ} {f : (Œ† i, X i) ‚Üí ‚Ñù‚â•0‚àû}
    (mf : Measurable[piFinset s] f) : DependsOn f s :=
  dependsOn_iff_factorsThrough.2 <| factorsThrough_of_measurable_comap mf

theorem lintegral_measurable_piFinset [DecidableEq Œπ] {s : Finset Œπ}
    {f : (Œ† i, X i) ‚Üí ‚Ñù‚â•0‚àû} (mf : Measurable[piFinset s] f)
    (x : Œ† i, X i) : ‚à´‚Åª y, f y ‚àÇproductMeasure Œº = (‚à´‚ãØ‚à´‚Åª_s, f ‚àÇŒº) x := by
  let g : (Œ† i : s, X i) ‚Üí ‚Ñù‚â•0‚àû := fun y ‚Ü¶ f (Function.updateFinset x _ y)
  have this y : g (s.restrict y) = f y := by
    refine measurable_dependsOn' mf fun i hi ‚Ü¶ ?_
    simp only [Function.updateFinset, Finset.restrict, dite_eq_ite, ite_eq_left_iff]
    exact fun h ‚Ü¶ (h hi).elim
  simp_rw [‚Üê this]
  rw [lintegral_dep]
  ¬∑ rfl
  ¬∑ exact mf.comp (measurable_updateFinset.mono le_rfl (piFinset.le s))

end ProductMeasure
