/-
Copyright (c) 2025 Etienne Marion. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Etienne Marion
-/
import Mathlib.Probability.Kernel.Composition.MeasureComp
import Mathlib.Probability.Kernel.IonescuTulcea.Traj

/-!
# Infinite product of probability measures

This files provides a definition for the product measure of an arbitrary family of probability
measures. Given `Î¼ : (i : Î¹) â†’ Measure (X i)`, `Measure.infinitePi Î¼` is the only probability
measure `Î½` over `Î  i, X i` such that `Î½ (Set.pi s t) = âˆ i âˆˆ s, Î¼ i (t i)`, with `s : Finset Î¹` and
such that `âˆ€ i âˆˆ s, MeasurableSet (t i)` (see `eq_infinitePi` and `infinitePi_boxes`).
We also provide a few results regarding integration against this measure.

## Main definition

* `Measure.infinitePi Î¼`: The product measure of the family of probability measures `Î¼`.

## Main statements

* `eq_infinitePi`: Any measure which gives to a finite product of sets the mass which is the
  product of their measures is the product measure.
* `infinitePi_boxes`: the product measure gives to finite products of sets a mass which is
  the product of their masses.
* `infinitePi_cylinder`: `infinitePi Î¼ (cylinder s S) = Measure.pi (fun i : s â†¦ Î¼ i) S`

## Implementation notes

To construct the product measure we first use the kernel `traj` obtained via the Ionescu-Tulcea
theorem to construct the measure over a product indexed by `â„•`, which is `infinitePiNat`. This
is an implementation detail and should not be used directly. Then we construct the product measure
over an arbitrary type by extending `piContent Î¼` thanks to CarathÃ©odory's theorem. The key lemma
to do so is `piContent_tendsto_zero`, which states that `piContent Î¼ (A n)` tends to zero if
`â‹‚ n, A n = âˆ…`. We prove this lemma by reducing to the case of an at most countable product,
in which case `piContent Î¼` is known to be a true measure (see `piContent_eq_measure_pi` and
`piContent_eq_infinitePiNat`).

## Tags

infinite product measure
-/

open MeasureTheory ProbabilityTheory Finset Filter Preorder MeasurableEquiv

open scoped ENNReal Topology

namespace MeasureTheory

section Preliminaries

variable {Î¹ : Type*} {Î± : Î¹ â†’ Type*}

variable {X : Î¹ â†’ Type*} [âˆ€ i, MeasurableSpace (X i)]
variable (Î¼ : (i : Î¹) â†’ Measure (X i)) [hÎ¼ : âˆ€ i, IsProbabilityMeasure (Î¼ i)]

/-- Consider a family of probability measures. You can take their products for any finite
subfamily. This gives a projective family of measures, see `IsProjectiveMeasureFamily`. -/
lemma isProjectiveMeasureFamily_pi :
    IsProjectiveMeasureFamily (fun I : Finset Î¹ â†¦ (Measure.pi (fun i : I â†¦ Î¼ i))) := by
  refine fun I J hJI â†¦ Measure.pi_eq (fun s ms â†¦ ?_)
  classical
  rw [Measure.map_apply (measurable_restrictâ‚‚ hJI) (.univ_pi ms),
    preimage_restrictâ‚‚ hJI, Measure.pi_pi]
  let g := fun i â†¦ (Î¼ i) (if hi : i âˆˆ J then s âŸ¨i, hiâŸ© else Set.univ)
  conv_lhs => change âˆ i : I, g i
  have h2 : univ.prod (fun i : J â†¦ (Î¼ i) (s i)) = univ.prod (fun i : J â†¦ g i) :=
    Finset.prod_congr rfl (by simp [g])
  rw [h2, prod_coe_sort, prod_coe_sort, prod_subset hJI (fun _ h h' â†¦ by simp [g, h, h'])]

/-- Consider a family of probability measures. You can take their products for any finite
subfamily. This gives an additive content on the measurable cylinders. -/
noncomputable def piContent : AddContent (measurableCylinders X) :=
  projectiveFamilyContent (isProjectiveMeasureFamily_pi Î¼)

lemma piContent_cylinder {I : Finset Î¹} {S : Set (Î  i : I, X i)} (hS : MeasurableSet S) :
    piContent Î¼ (cylinder I S) = Measure.pi (fun i : I â†¦ Î¼ i) S :=
  projectiveFamilyContent_cylinder _ hS

theorem piContent_eq_measure_pi [Fintype Î¹] {s : Set (Î  i, X i)} (hs : MeasurableSet s) :
    piContent Î¼ s = Measure.pi Î¼ s := by
  let aux : (Î  i : univ, X i) â†’ (Î  i, X i) := fun x i â†¦ x âŸ¨i, mem_univ iâŸ©
  have maux : Measurable aux := measurable_pi_lambda _ (fun _ â†¦ measurable_pi_apply _)
  have pi_eq : Measure.pi Î¼ = (Measure.pi (fun i : univ â†¦ Î¼ i)).map aux := by
    refine Measure.pi_eq fun a ma â†¦ ?_
    rw [Measure.map_apply maux (.univ_pi ma)]
    have : aux â»Â¹' Set.univ.pi a = Set.univ.pi (fun i : @univ Î¹ _ â†¦ a i) := by ext x; simp [aux]
    rw [this, Measure.pi_pi]
    simp
  have : s = cylinder univ (aux â»Â¹' s) := by ext x; simp [aux]
  nth_rw 1 [this]
  rw [piContent_cylinder _ (maux hs), pi_eq, Measure.map_apply maux hs]

end Preliminaries

section Nat

open Kernel

/-! ### Product of measures indexed by `â„•` -/

variable {X : â„• â†’ Type*}

lemma _root_.IocProdIoc_preim {a b c : â„•} (hab : a â‰¤ b) (hbc : b â‰¤ c)
    (s : (i : Ioc a c) â†’ Set (X i)) :
    IocProdIoc a b c â»Â¹' (Set.univ.pi s) =
      (Set.univ.pi <| restrictâ‚‚ (Ï€ := (fun n â†¦ Set (X n))) (Ioc_subset_Ioc_right hbc) s) Ã—Ë¢
        (Set.univ.pi <| restrictâ‚‚ (Ï€ := (fun n â†¦ Set (X n))) (Ioc_subset_Ioc_left hab) s) := by
  ext x
  simp only [Set.mem_preimage, Set.mem_pi, Set.mem_univ, IocProdIoc, forall_const, Subtype.forall,
    mem_Ioc, Set.mem_prod, restrictâ‚‚]
  refine âŸ¨fun h â†¦ âŸ¨fun i âŸ¨hi1, hi2âŸ© â†¦ ?_, fun i âŸ¨hi1, hi2âŸ© â†¦ ?_âŸ©, fun âŸ¨h1, h2âŸ© i âŸ¨hi1, hi2âŸ© â†¦ ?_âŸ©
  Â· convert h i âŸ¨hi1, hi2.trans hbcâŸ©
    rw [dif_pos hi2]
  Â· convert h i âŸ¨lt_of_le_of_lt hab hi1, hi2âŸ©
    rw [dif_neg (not_le.2 hi1)]
  Â· split_ifs with hi3
    Â· exact h1 i âŸ¨hi1, hi3âŸ©
    Â· exact h2 i âŸ¨not_le.1 hi3, hi2âŸ©

lemma _root_.IicProdIoc_preim {a b : â„•} (hab : a â‰¤ b) (s : (i : Iic b) â†’ Set (X i)) :
    IicProdIoc a b â»Â¹' (Set.univ.pi s) =
      (Set.univ.pi <| frestrictLeâ‚‚ (Ï€ := (fun n â†¦ Set (X n))) hab s) Ã—Ë¢
        (Set.univ.pi <| restrictâ‚‚ (Ï€ := (fun n â†¦ Set (X n))) Ioc_subset_Iic_self s) := by
  ext x
  simp only [Set.mem_preimage, Set.mem_pi, Set.mem_univ, IicProdIoc_def, forall_const,
    Subtype.forall, mem_Iic, Set.mem_prod, frestrictLeâ‚‚_apply, restrictâ‚‚, mem_Ioc]
  refine âŸ¨fun h â†¦ âŸ¨fun i hi â†¦ ?_, fun i âŸ¨hi1, hi2âŸ© â†¦ ?_âŸ©, fun âŸ¨h1, h2âŸ© i hi â†¦ ?_âŸ©
  Â· convert h i (hi.trans hab)
    rw [dif_pos hi]
  Â· convert h i hi2
    rw [dif_neg (not_le.2 hi1)]
  Â· split_ifs with hi3
    Â· exact h1 i hi3
    Â· exact h2 i âŸ¨not_le.1 hi3, hiâŸ©

variable [âˆ€ n, MeasurableSpace (X n)]
  (Î¼ : (n : â„•) â†’ Measure (X n)) [hÎ¼ : âˆ€ n, IsProbabilityMeasure (Î¼ n)]

/-- Infinite product measure indexed by `â„•`. This is an auxiliary construction, you should use
the generic product measure `Measure.infinitePi`. -/
noncomputable def Measure.infinitePiNat : Measure ((n : â„•) â†’ X n) :=
  (traj (fun n â†¦ const _ (Î¼ (n + 1))) 0) âˆ˜â‚˜ (Measure.pi (fun i : Iic 0 â†¦ Î¼ i))

instance : IsProbabilityMeasure (Measure.infinitePiNat Î¼) := by
  rw [Measure.infinitePiNat]; infer_instance

lemma Measure.pi_prod_map_IocProdIoc {a b c : â„•} (hab : a â‰¤ b) (hbc : b â‰¤ c) :
    ((Measure.pi (fun i : Ioc a b â†¦ Î¼ i)).prod (Measure.pi (fun i : Ioc b c â†¦ Î¼ i))).map
      (IocProdIoc a b c) = Measure.pi (fun i : Ioc a c â†¦ Î¼ i) := by
  refine (Measure.pi_eq fun s ms â†¦ ?_).symm
  simp_rw [Measure.map_apply measurable_IocProdIoc (.univ_pi ms), IocProdIoc_preim hab hbc,
    Measure.prod_prod, Measure.pi_pi, prod_eq_prod_extend]
  nth_rw 1 [Eq.comm, â† Ioc_union_Ioc_eq_Ioc hab hbc, prod_union (Ioc_disjoint_Ioc a b c)]
  congr 1 <;> refine prod_congr rfl fun x hx â†¦ ?_
  Â· rw [Function.extend_val_apply x hx, Function.extend_val_apply x (Ioc_subset_Ioc_right hbc hx),
    restrictâ‚‚]
  Â· rw [Function.extend_val_apply x hx, Function.extend_val_apply x (Ioc_subset_Ioc_left hab hx),
    restrictâ‚‚]

lemma Measure.pi_prod_map_IicProdIoc {a b : â„•} :
    ((Measure.pi (fun i : Iic a â†¦ Î¼ i)).prod (Measure.pi (fun i : Ioc a b â†¦ Î¼ i))).map
      (IicProdIoc a b) = Measure.pi (fun i : Iic b â†¦ Î¼ i) := by
  obtain hab | hba := le_total a b
  Â· refine (Measure.pi_eq fun s ms â†¦ ?_).symm
    simp_rw [Measure.map_apply measurable_IicProdIoc (.univ_pi ms), IicProdIoc_preim hab,
      Measure.prod_prod, Measure.pi_pi, prod_eq_prod_extend]
    nth_rw 1 [Eq.comm, â† Iic_union_Ioc_eq_Iic hab, prod_union (Iic_disjoint_Ioc le_rfl)]
    congr 1 <;> refine prod_congr rfl fun x hx â†¦ ?_
    Â· rw [Function.extend_val_apply x hx, Function.extend_val_apply x (Iic_subset_Iic.2 hab hx),
      frestrictLeâ‚‚, restrictâ‚‚]
    Â· rw [Function.extend_val_apply x hx, Function.extend_val_apply x (Ioc_subset_Iic_self hx),
      restrictâ‚‚]
  Â· rw [IicProdIoc_le hba, â† Measure.map_map, â† Measure.fst, Measure.fst_prod]
    Â· exact isProjectiveMeasureFamily_pi Î¼ (Iic a) (Iic b) (Iic_subset_Iic.2 hba) |>.symm
    all_goals fun_prop

lemma Measure.map_piSingleton (Î¼ : (n : â„•) â†’ Measure (X n)) [âˆ€ n, SigmaFinite (Î¼ n)] (n : â„•) :
    (Î¼ (n + 1)).map (piSingleton n) = Measure.pi (fun i : Ioc n (n + 1) â†¦ Î¼ i) := by
  refine (Measure.pi_eq fun s hs â†¦ ?_).symm
  have : Subsingleton (Ioc n (n + 1)) := by
    rw [Nat.Ioc_succ_singleton]
    infer_instance
  rw [Fintype.prod_subsingleton _ âŸ¨n + 1, mem_Ioc.2 (by omega)âŸ©, Measure.map_apply]
  Â· congr with x
    simp only [Set.mem_preimage, Set.mem_pi, Set.mem_univ, forall_const, Subtype.forall,
      Nat.Ioc_succ_singleton, mem_singleton]
    exact âŸ¨fun h â†¦ h (n + 1) rfl, fun h a b â†¦ b.symm â–¸ hâŸ©
  Â· exact (piSingleton n).measurable
  Â· exact .univ_pi hs

theorem partialTraj_const_restrictâ‚‚ {a b : â„•} :
    (partialTraj (fun n â†¦ const _ (Î¼ (n + 1))) a b).map (restrictâ‚‚ (Ioc_subset_Iic_self (a := a))) =
    const _ (Measure.pi (fun i : Ioc a b â†¦ Î¼ i)) := by
  obtain hab | hba := lt_or_le a b
  Â· refine Nat.le_induction ?_ (fun n hn hind â†¦ ?_) b (Nat.succ_le.2 hab) <;> ext1 xâ‚€
    Â· rw [partialTraj_succ_self, â† Kernel.map_comp_right, map_apply, prod_apply, map_apply,
        const_apply, const_apply, Measure.map_piSingleton, restrictâ‚‚_comp_IicProdIoc,
        Measure.map_snd_prod, measure_univ, one_smul]
      all_goals fun_prop
    Â· have : (restrictâ‚‚ (Ioc_subset_Iic_self (a := a))) âˆ˜ (IicProdIoc (X := X) n (n + 1)) =
          (IocProdIoc a n (n + 1)) âˆ˜ (Prod.map (restrictâ‚‚ Ioc_subset_Iic_self) id) := rfl
      rw [const_apply, partialTraj_succ_of_le (by omega), map_const, prod_const_comp, id_comp,
        â† map_comp_right, this, map_comp_right, â† map_prod_map, hind, Kernel.map_id, map_apply,
        prod_apply, const_apply, const_apply, Measure.map_piSingleton,
        Measure.pi_prod_map_IocProdIoc]
      any_goals fun_prop
      all_goals omega
  Â· have : IsEmpty (Ioc a b) := by simpa [hba] using Subtype.isEmpty_false
    ext x s ms
    by_cases hs : s.Nonempty
    Â· rw [Subsingleton.eq_univ_of_nonempty hs, @measure_univ .., measure_univ]
      exact (IsMarkovKernel.map _ (measurable_restrictâ‚‚ _)) |>.isProbabilityMeasure x
    Â· rw [Set.not_nonempty_iff_eq_empty.1 hs]
      simp

theorem partialTraj_const {a b : â„•} :
    partialTraj (fun n â†¦ const _ (Î¼ (n + 1))) a b =
      (Kernel.id Ã—â‚– (const _ (Measure.pi (fun i : Ioc a b â†¦ Î¼ i)))).map (IicProdIoc a b) := by
  rw [partialTraj_eq_prod, partialTraj_const_restrictâ‚‚]

theorem isProjectiveLimit_infinitePiNat :
    IsProjectiveLimit (Measure.infinitePiNat Î¼)
      (fun I : Finset â„• â†¦ (Measure.pi (fun i : I â†¦ Î¼ i))) := by
  have _ := ProbabilityMeasure.nonempty âŸ¨Î¼ 0, hÎ¼ 0âŸ©
  intro I
  simp_rw [isProjectiveMeasureFamily_pi Î¼ _ _ I.subset_Iic_sup_id]
  rw [â† restrictâ‚‚_comp_restrict I.subset_Iic_sup_id, â† Measure.map_map, â† frestrictLe,
    Measure.infinitePiNat, Measure.map_comp, traj_map_frestrictLe, partialTraj_const,
    â† Measure.map_comp, â† Measure.compProd_eq_comp_prod, Measure.compProd_const,
    Measure.pi_prod_map_IicProdIoc]
  all_goals fun_prop

lemma infinitePiNat_map_restrict {I : Finset â„•} :
    (Measure.infinitePiNat Î¼).map I.restrict = Measure.pi fun i : I â†¦ Î¼ i :=
  isProjectiveLimit_infinitePiNat Î¼ I

theorem piContent_eq_infinitePiNat {A : Set ((n : â„•) â†’ X n)} (hA : A âˆˆ measurableCylinders X) :
    piContent Î¼ A = Measure.infinitePiNat Î¼ A := by
  obtain âŸ¨s, S, mS, rflâŸ© : âˆƒ s S, MeasurableSet S âˆ§ A = cylinder s S := by
    simpa [mem_measurableCylinders] using hA
  rw [piContent_cylinder _ mS, cylinder, â† Measure.map_apply (measurable_restrict _) mS,
    isProjectiveLimit_infinitePiNat Î¼]

end Nat

section InfinitePi

open Measure

/-! ### Product of infinitely many probability measures -/

variable {Î¹ : Type*} {X : Î¹ â†’ Type*} [âˆ€ i, MeasurableSpace (X i)]
  (Î¼ : (i : Î¹) â†’ Measure (X i)) [hÎ¼ : âˆ€ i, IsProbabilityMeasure (Î¼ i)]

lemma infinitePiNat_map_piCongrLeft (e : â„• â‰ƒ Î¹) {s : Set (Î  i, X i)}
    (hs : s âˆˆ measurableCylinders X) :
    (infinitePiNat (fun n â†¦ Î¼ (e n))).map (piCongrLeft X e) s = piContent Î¼ s := by
  obtain âŸ¨I, S, hS, rflâŸ© := (mem_measurableCylinders s).1 hs
  rw [map_apply _ hS.cylinder, cylinder, â† Set.preimage_comp, coe_piCongrLeft,
    restrict_comp_piCongrLeft, Set.preimage_comp, â† map_apply,
    infinitePiNat_map_restrict (fun n â†¦ Î¼ (e n)), â† cylinder, piContent_cylinder Î¼ hS,
    â† pi_map_piCongrLeft (e.frestrict I), map_apply _ hS]
  Â· rfl
  any_goals fun_prop
  exact hS.preimage (by fun_prop)

/-- This is the key theorem to build the product of an arbitrary family of probability measures:
the `piContent` of a decreasing sequence of cylinders with empty intersection converges to `0`.

This implies the `Ïƒ`-additivity of `piContent` (see `addContent_iUnion_eq_sum_of_tendsto_zero`),
which allows to extend it to the `Ïƒ`-algebra by CarathÃ©odory's theorem. -/
theorem piContent_tendsto_zero {A : â„• â†’ Set (Î  i, X i)} (A_mem : âˆ€ n, A n âˆˆ measurableCylinders X)
    (A_anti : Antitone A) (A_inter : â‹‚ n, A n = âˆ…) :
    Tendsto (fun n â†¦ piContent Î¼ (A n)) atTop (ğ“ 0) := by
  have : âˆ€ i, Nonempty (X i) := fun i â†¦ ProbabilityMeasure.nonempty âŸ¨Î¼ i, hÎ¼ iâŸ©
  have A_cyl n : âˆƒ s S, MeasurableSet S âˆ§ A n = cylinder s S :=
    (mem_measurableCylinders _).1 (A_mem n)
  choose s S mS A_eq using A_cyl
  -- The family `(Aâ‚™)` only depends on a countable set of coordinates, called `u`. Therefore our
  -- goal is to see it as a family indexed by this countable set, because on the product indexed
  -- by this countable set we can build a measure. To do so we have to pull back our cylinders
  -- along the injection from `Î  i : u, X i` to `Î  i, X i`.
  let u := â‹ƒ n, (s n).toSet
  let Î½ := fun i : u â†¦ Î¼ i
  -- `tâ‚™` will be `sâ‚™` seen as a subset of `u`.
  let t n : Finset u := (s n).preimage Subtype.val Subtype.val_injective.injOn
  classical
  -- The map `f` allows to pull back `Aâ‚™`
  let f : (Î  i : u, X i) â†’ Î  i, X i :=
    fun x i â†¦ if hi : i âˆˆ u then x âŸ¨i, hiâŸ© else Classical.ofNonempty
  -- `aux` is the obvious equivalence between `sâ‚™` and `tâ‚™`
  let aux n : t n â‰ƒ s n :=
    { toFun := fun i â†¦ âŸ¨i.1.1, mem_preimage.1 i.2âŸ©
      invFun := fun i â†¦ âŸ¨âŸ¨i.1, Set.mem_iUnion.2 âŸ¨n, i.2âŸ©âŸ©, mem_preimage.2 i.2âŸ©
      left_inv := fun i â†¦ by simp
      right_inv := fun i â†¦ by simp }
  -- Finally `gâ‚™` is the equivalence between the product indexed by `tâ‚™` and the one indexed by `sâ‚™`
  let g n := (aux n).piCongrLeft (fun i : s n â†¦ X i)
  -- Mapping from the product indexed by `u` by `f` and then restricting to `sâ‚™` is the same as
  -- first restricting to `tâ‚™` and then mapping by `gâ‚™`
  have r_comp_f n : (s n).restrict âˆ˜ f = (g n) âˆ˜ (fun (x : Î  i : u, X i) i â†¦ x i) := by
    ext x i
    simp only [Function.comp_apply, Finset.restrict, subset_refl, Set.coe_inclusion,
      Equiv.piCongrLeft_apply, Equiv.coe_fn_symm_mk, f, aux, g, t]
    rw [dif_pos (Set.mem_iUnion.2 âŸ¨n, i.2âŸ©)]
  -- `Bâ‚™` is the same as `Aâ‚™` but in the product indexed by `u`
  let B n := f â»Â¹' (A n)
  -- `Tâ‚™` is the same as `Sâ‚™` but in the product indexed by `u`
  let T n := (g n) â»Â¹' (S n)
  -- We now tranfer the properties of `Aâ‚™` and `Sâ‚™` to `Bâ‚™` and `Tâ‚™`
  have B_eq n : B n = cylinder (t n) (T n) := by
    simp_rw [B, A_eq, cylinder, â† Set.preimage_comp, r_comp_f]; rfl
  have mg n : Measurable (g n) := by fun_prop
  have mT n : MeasurableSet (T n) := (mS n).preimage (mg n)
  have B_mem n : B n âˆˆ measurableCylinders (fun i : u â†¦ X i) :=
    (mem_measurableCylinders (B n)).2 âŸ¨t n, T n, mT n, B_eq nâŸ©
  have mB n : MeasurableSet (B n) := .of_mem_measurableCylinders (B_mem n)
  have B_anti : Antitone B := fun m n hmn â†¦ Set.preimage_mono <| A_anti hmn
  have B_inter : â‹‚ n, B n = âˆ… := by
    simp_rw [B, â† Set.preimage_iInter, A_inter, Set.preimage_empty]
  -- We now rewrite `piContent Î¼ (A n)` as `piContent (fun i : u â†¦ Î¼ i) (B n)`. Then there are two
  -- cases: either `u` is finite and we rewrite it to the finite product measure, either
  -- it is countable and we rewrite it to the pushforward measure of `infinitePiNat`. In both cases
  -- we have an actual measure and we can conclude with `tendsto_measure_iInter_atTop`.
  conv =>
    enter [1]; ext n
    rw [A_eq, piContent_cylinder Î¼ (mS n), â† pi_map_piCongrLeft (aux n),
      map_apply (by fun_prop) (mS n)]
    change (Measure.pi (fun i : t n â†¦ Î¼ i)) (T n)
    rw [â† piContent_cylinder (fun i : u â†¦ Î¼ i) (mT n), â† B_eq n]
  obtain u_fin | u_inf := finite_or_infinite u
  Â· let _ := Fintype.ofFinite u
    simp_rw [fun n â†¦ piContent_eq_measure_pi (fun i : u â†¦ Î¼ i) (mB n)]
    convert tendsto_measure_iInter_atTop (fun n â†¦ (mB n).nullMeasurableSet) B_anti
      âŸ¨0, measure_ne_top _ _âŸ©
    Â· rw [B_inter, measure_empty]
    Â· infer_instance
  Â· -- If `u` is infinite, then we have an equivalence with `â„•` so we can apply `secondLemma`.
    have count_u : Countable u := Set.countable_iUnion (fun n â†¦ (s n).countable_toSet)
    obtain âŸ¨Ï†, -âŸ© := Classical.exists_true_of_nonempty (Î± := â„• â‰ƒ u) nonempty_equiv_of_countable
    conv => enter [1]; ext n; rw [â† infinitePiNat_map_piCongrLeft _ Ï† (B_mem n)]
    convert tendsto_measure_iInter_atTop (fun n â†¦ (mB n).nullMeasurableSet) B_anti
      âŸ¨0, measure_ne_top _ _âŸ©
    Â· rw [B_inter, measure_empty]
    Â· infer_instance

/-- The `projectiveFamilyContent` associated to a family of probability measures is
Ïƒ-subadditive. -/
theorem isSigmaSubadditive_piContent : (piContent Î¼).IsSigmaSubadditive := by
  refine isSigmaSubadditive_of_addContent_iUnion_eq_tsum
    isSetRing_measurableCylinders (fun f hf hf_Union hf' â†¦ ?_)
  exact addContent_iUnion_eq_sum_of_tendsto_zero isSetRing_measurableCylinders
    (piContent Î¼) (fun s hs â†¦ projectiveFamilyContent_ne_top _)
    (fun _ â†¦ piContent_tendsto_zero Î¼) hf hf_Union hf'

/-- The product measure of an arbitrary family of probability measures. It is defined as the unique
extension of the function which gives to cylinders the measure given by the associated product
measure. -/
noncomputable def Measure.infinitePi : Measure (Î  i, X i) :=
  (piContent Î¼).measure isSetSemiring_measurableCylinders
    generateFrom_measurableCylinders.ge (isSigmaSubadditive_piContent Î¼)

open Measure

/-- The product measure is the projective limit of the partial product measures. This ensures
uniqueness and expresses the value of the product measures applied to cylinders. -/
theorem isProjectiveLimit_infinitePi :
    IsProjectiveLimit (infinitePi Î¼) (fun I : Finset Î¹ â†¦ (Measure.pi (fun i : I â†¦ Î¼ i))) := by
  intro I
  ext1 s hs
  rw [map_apply (measurable_restrict I) hs, infinitePi, AddContent.measure_eq,
    â† cylinder, piContent_cylinder Î¼ hs]
  Â· exact generateFrom_measurableCylinders.symm
  Â· exact cylinder_mem_measurableCylinders _ _ hs

instance : IsProbabilityMeasure (infinitePi Î¼) := by
  constructor
  rw [â† cylinder_univ âˆ…, cylinder, â† map_apply (measurable_restrict _) .univ,
    isProjectiveLimit_infinitePi Î¼, measure_univ]

theorem eq_infinitePi {Î½ : Measure (Î  i, X i)}
    (hÎ½ : âˆ€ s : Finset Î¹, âˆ€ t : (i : Î¹) â†’ Set (X i),
      (âˆ€ i âˆˆ s, MeasurableSet (t i)) â†’ Î½ (Set.pi s t) = âˆ i âˆˆ s, Î¼ i (t i)) :
    Î½ = infinitePi Î¼ := by
  refine (isProjectiveLimit_infinitePi Î¼).unique ?_ |>.symm
  refine fun s â†¦ (pi_eq fun t ht â†¦ ?_).symm
  classical
  rw [Measure.map_apply, preimage_restrict, hÎ½, â† prod_attach, univ_eq_attach]
  Â· congr with i
    rw [dif_pos i.2]
  any_goals fun_prop
  Â· exact fun i hi â†¦ dif_pos hi â–¸ ht âŸ¨i, hiâŸ©
  Â· exact .univ_pi ht

-- TODO: add a version for an infinite product
lemma infinitePi_boxes {s : Finset Î¹} {t : (i : Î¹) â†’ Set (X i)}
    (mt : âˆ€ i âˆˆ s, MeasurableSet (t i)) :
    infinitePi Î¼ (Set.pi s t) = âˆ i âˆˆ s, (Î¼ i) (t i) := by
  have : Set.pi s t = cylinder s ((@Set.univ s).pi (fun i : s â†¦ t i)) := by
    ext x
    simp
  rw [this, cylinder, â† map_apply, isProjectiveLimit_infinitePi Î¼, pi_pi]
  Â· rw [Finset.univ_eq_attach, Finset.prod_attach _ (fun i â†¦ (Î¼ i) (t i))]
  Â· exact measurable_restrict _
  Â· exact .univ_pi fun i â†¦ mt i.1 i.2

theorem infinitePi_eq_pi [Fintype Î¹] : infinitePi Î¼ = Measure.pi Î¼ := by
  refine (pi_eq fun s hs â†¦ ?_).symm
  rw [â† coe_univ, infinitePi_boxes]
  simpa

lemma infinitePi_cylinder {s : Finset Î¹} {S : Set (Î  i : s, X i)} (mS : MeasurableSet S) :
    infinitePi Î¼ (cylinder s S) = Measure.pi (fun i : s â†¦ Î¼ i) S := by
  rw [cylinder, â† Measure.map_apply (measurable_restrict _) mS, isProjectiveLimit_infinitePi Î¼]

theorem integral_dep_infinitePi {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
    {s : Finset Î¹} {f : (Î  i : s, X i) â†’ E}
    (hf : AEStronglyMeasurable f (Measure.pi (fun i : s â†¦ Î¼ i))) :
    âˆ« y, f (s.restrict y) âˆ‚infinitePi Î¼ = âˆ« y, f y âˆ‚Measure.pi (fun i : s â†¦ Î¼ i) := by
  rw [â† integral_map, isProjectiveLimit_infinitePi Î¼]
  Â· fun_prop
  Â· rwa [isProjectiveLimit_infinitePi Î¼]

open Filtration

/-- If a function is strongly measurable with respect to the Ïƒ-algebra generated by
the finite set of coordinates `s`, then it only depends on those coordinates. -/
theorem dependsOn_of_stronglyMeasurable' {E : Type*} [NormedAddCommGroup E]
    {s : Finset Î¹} {f : (Î  i, X i) â†’ E}
    (mf : StronglyMeasurable[piFinset s] f) : DependsOn f s :=
  dependsOn_iff_factorsThrough.2 mf.factorsThrough

theorem integral_stronglyMeasurable [DecidableEq Î¹] {E : Type*} [NormedAddCommGroup E]
    [NormedSpace â„ E] {s : Finset Î¹} {f : (Î  i, X i) â†’ E}
    (mf : StronglyMeasurable[piFinset s] f) (x : Î  i, X i) :
    âˆ« y, f y âˆ‚infinitePi Î¼ =
    âˆ« y, f (Function.updateFinset x s y) âˆ‚Measure.pi (fun i : s â†¦ Î¼ i) := by
  let g : (Î  i : s, X i) â†’ E := fun y â†¦ f (Function.updateFinset x _ y)
  have this y : g (s.restrict y) = f y := by
    apply dependsOn_of_stronglyMeasurable' mf
    intro i hi
    simp only [Function.updateFinset, Finset.restrict, dite_eq_ite, ite_eq_left_iff]
    exact fun h â†¦ (h hi).elim
  rw [â† integral_congr_ae <| Eventually.of_forall this, integral_dep_infinitePi]
  exact mf.comp_measurable (measurable_updateFinset.mono le_rfl (piFinset.le s))
    |>.aestronglyMeasurable

theorem lintegral_dep {s : Finset Î¹} {f : (Î  i : s, X i) â†’ â„â‰¥0âˆ} (hf : Measurable f) :
    âˆ«â» y, f (s.restrict y) âˆ‚infinitePi Î¼ = âˆ«â» y, f y âˆ‚Measure.pi (fun i : s â†¦ Î¼ i) := by
  rw [â† lintegral_map hf (measurable_restrict _), isProjectiveLimit_infinitePi Î¼]

theorem lintegral_measurable_piFinset [DecidableEq Î¹] {s : Finset Î¹}
    {f : (Î  i, X i) â†’ â„â‰¥0âˆ} (mf : Measurable[piFinset s] f)
    (x : Î  i, X i) : âˆ«â» y, f y âˆ‚infinitePi Î¼ = (âˆ«â‹¯âˆ«â»_s, f âˆ‚Î¼) x := by
  let g : (Î  i : s, X i) â†’ â„â‰¥0âˆ := fun y â†¦ f (Function.updateFinset x _ y)
  have this y : g (s.restrict y) = f y := by
    refine mf.dependsOn_of_piFinset fun i hi â†¦ ?_
    simp only [Function.updateFinset, Finset.restrict, dite_eq_ite, ite_eq_left_iff]
    exact fun h â†¦ (h hi).elim
  simp_rw [â† this]
  rw [lintegral_dep]
  Â· rfl
  Â· exact mf.comp (measurable_updateFinset.mono le_rfl (piFinset.le s))

end InfinitePi

end MeasureTheory
