/-
Copyright (c) 2025 Etienne Marion. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Etienne Marion
-/
import Mathlib.Probability.Kernel.Composition.MeasureComp
import Mathlib.Probability.Kernel.IonescuTulcea.Traj

/-!
# Infinite product of probability measures

This file provides a definition for the product measure of an arbitrary family of probability
measures. Given `Œº : (i : Œπ) ‚Üí Measure (X i)` such that each `Œº i` is a probability measure,
`Measure.infinitePi Œº` is the only probability measure `ŒΩ` over `Œ† i, X i` such that
`ŒΩ (Set.pi s t) = ‚àè i ‚àà s, Œº i (t i)`, with `s : Finset Œπ` and
such that `‚àÄ i ‚àà s, MeasurableSet (t i)` (see `eq_infinitePi` and `infinitePi_pi`).
We also provide a few results regarding integration against this measure.

## Main definition

* `Measure.infinitePi Œº`: The product measure of the family of probability measures `Œº`.

## Main statements

* `eq_infinitePi`: Any measure which gives to a finite product of sets the mass which is the
  product of their measures is the product measure.
* `infinitePi_pi`: the product measure gives to finite products of sets a mass which is
  the product of their masses.
* `infinitePi_cylinder`: `infinitePi Œº (cylinder s S) = Measure.pi (fun i : s ‚Ü¶ Œº i) S`

## Implementation notes

To construct the product measure we first use the kernel `traj` obtained via the Ionescu-Tulcea
theorem to construct the measure over a product indexed by `‚Ñï`, which is `infinitePiNat`. This
is an implementation detail and should not be used directly. Then we construct the product measure
over an arbitrary type by extending `piContent Œº` thanks to Carath√©odory's theorem. The key lemma
to do so is `piContent_tendsto_zero`, which states that `piContent Œº (A n)` tends to zero if
`A` is a non increasing sequence of sets satisfying `‚ãÇ n, A n = ‚àÖ`.
We prove this lemma by reducing to the case of an at most countable product,
in which case `piContent Œº` is known to be a true measure (see `piContent_eq_measure_pi` and
`piContent_eq_infinitePiNat`).

## Tags

infinite product measure
-/

open ProbabilityTheory Finset Filter Preorder MeasurableEquiv

open scoped ENNReal Topology

namespace MeasureTheory

section Preliminaries

variable {Œπ : Type*} {X : Œπ ‚Üí Type*} {mX : ‚àÄ i, MeasurableSpace (X i)}
variable (Œº : (i : Œπ) ‚Üí Measure (X i)) [hŒº : ‚àÄ i, IsProbabilityMeasure (Œº i)]

/-- Consider a family of probability measures. You can take their products for any finite
subfamily. This gives a projective family of measures. -/
lemma isProjectiveMeasureFamily_pi :
    IsProjectiveMeasureFamily (fun I : Finset Œπ ‚Ü¶ (Measure.pi (fun i : I ‚Ü¶ Œº i))) := by
  refine fun I J hJI ‚Ü¶ Measure.pi_eq (fun s ms ‚Ü¶ ?_)
  classical
  simp_rw [Measure.map_apply (measurable_restrict‚ÇÇ hJI) (.univ_pi ms), restrict‚ÇÇ_preimage hJI,
    Measure.pi_pi, prod_eq_prod_extend]
  refine (prod_subset_one_on_sdiff hJI (fun x hx ‚Ü¶ ?_) (fun x hx ‚Ü¶ ?_)).symm
  ¬∑ rw [Function.extend_val_apply (mem_sdiff.1 hx).1, dif_neg (mem_sdiff.1 hx).2, measure_univ]
  ¬∑ rw [Function.extend_val_apply hx, Function.extend_val_apply (hJI hx), dif_pos hx]

/-- Consider a family of probability measures. You can take their products for any finite
subfamily. This gives an additive content on the measurable cylinders. -/
noncomputable def piContent : AddContent (measurableCylinders X) :=
  projectiveFamilyContent (isProjectiveMeasureFamily_pi Œº)

lemma piContent_cylinder {I : Finset Œπ} {S : Set (Œ† i : I, X i)} (hS : MeasurableSet S) :
    piContent Œº (cylinder I S) = Measure.pi (fun i : I ‚Ü¶ Œº i) S :=
  projectiveFamilyContent_cylinder _ hS

theorem piContent_eq_measure_pi [Fintype Œπ] {s : Set (Œ† i, X i)} (hs : MeasurableSet s) :
    piContent Œº s = Measure.pi Œº s := by
  let e : @Finset.univ Œπ _ ‚âÉ Œπ :=
    { toFun i := i
      invFun i := ‚ü®i, mem_univ i‚ü© }
  have : s = cylinder univ (MeasurableEquiv.piCongrLeft X e ‚Åª¬π' s) := rfl
  nth_rw 1 [this]
  dsimp [e]
  rw [piContent_cylinder _ (hs.preimage (by fun_prop)), ‚Üê Measure.pi_map_piCongrLeft e,
    ‚Üê Measure.map_apply (by fun_prop) hs]; rfl

end Preliminaries

section Nat

open Kernel

/-! ### Product of measures indexed by `‚Ñï` -/

variable {X : ‚Ñï ‚Üí Type*}

variable {mX : ‚àÄ n, MeasurableSpace (X n)}
  (Œº : (n : ‚Ñï) ‚Üí Measure (X n)) [hŒº : ‚àÄ n, IsProbabilityMeasure (Œº n)]

namespace Measure

/-- Infinite product measure indexed by `‚Ñï`. This is an auxiliary construction, you should use
the generic product measure `Measure.infinitePi`. -/
noncomputable def infinitePiNat : Measure (Œ† n, X n) :=
  (traj (fun n ‚Ü¶ const _ (Œº (n + 1))) 0) ‚àò‚Çò (Measure.pi (fun i : Iic 0 ‚Ü¶ Œº i))

instance : IsProbabilityMeasure (Measure.infinitePiNat Œº) := by
  rw [Measure.infinitePiNat]; infer_instance

/-- Let `Œº : (i : Ioc a c) ‚Üí Measure (X i)` be a family of measures. Up to an equivalence,
`(‚®Ç i : Ioc a b, Œº i) ‚äó (‚®Ç i : Ioc b c, Œº i) = ‚®Ç i : Ioc a c, Œº i`, where `‚äó` denotes the
product of measures. -/
lemma pi_prod_map_IocProdIoc {a b c : ‚Ñï} (hab : a ‚â§ b) (hbc : b ‚â§ c) :
    ((Measure.pi (fun i : Ioc a b ‚Ü¶ Œº i)).prod (Measure.pi (fun i : Ioc b c ‚Ü¶ Œº i))).map
      (IocProdIoc a b c) = Measure.pi (fun i : Ioc a c ‚Ü¶ Œº i) := by
  refine (Measure.pi_eq fun s ms ‚Ü¶ ?_).symm
  simp_rw [Measure.map_apply measurable_IocProdIoc (.univ_pi ms), IocProdIoc_preimage hab hbc,
    Measure.prod_prod, Measure.pi_pi, prod_eq_prod_extend]
  nth_rw 1 [Eq.comm, ‚Üê Ioc_union_Ioc_eq_Ioc hab hbc, prod_union (Ioc_disjoint_Ioc_of_le le_rfl)]
  congr 1 <;> refine prod_congr rfl fun x hx ‚Ü¶ ?_
  ¬∑ rw [Function.extend_val_apply hx, Function.extend_val_apply (Ioc_subset_Ioc_right hbc hx),
      restrict‚ÇÇ]
  ¬∑ rw [Function.extend_val_apply hx, Function.extend_val_apply (Ioc_subset_Ioc_left hab hx),
      restrict‚ÇÇ]

/-- Let `Œº : (i : Iic b) ‚Üí Measure (X i)` be a family of measures. Up to an equivalence,
`(‚®Ç i : Iic a, Œº i) ‚äó (‚®Ç i : Ioc a b, Œº i) = ‚®Ç i : Iic b, Œº i`, where `‚äó` denotes the
product of measures. -/
lemma pi_prod_map_IicProdIoc {a b : ‚Ñï} :
    ((Measure.pi (fun i : Iic a ‚Ü¶ Œº i)).prod (Measure.pi (fun i : Ioc a b ‚Ü¶ Œº i))).map
      (IicProdIoc a b) = Measure.pi (fun i : Iic b ‚Ü¶ Œº i) := by
  obtain hab | hba := le_total a b
  ¬∑ refine (Measure.pi_eq fun s ms ‚Ü¶ ?_).symm
    simp_rw [Measure.map_apply measurable_IicProdIoc (.univ_pi ms), IicProdIoc_preimage hab,
      Measure.prod_prod, Measure.pi_pi, prod_eq_prod_extend]
    nth_rw 1 [Eq.comm, ‚Üê Iic_union_Ioc_eq_Iic hab, prod_union (Iic_disjoint_Ioc le_rfl)]
    congr 1 <;> refine prod_congr rfl fun x hx ‚Ü¶ ?_
    ¬∑ rw [Function.extend_val_apply hx, Function.extend_val_apply (Iic_subset_Iic.2 hab hx),
        frestrictLe‚ÇÇ, restrict‚ÇÇ]
    ¬∑ rw [Function.extend_val_apply hx, Function.extend_val_apply (Ioc_subset_Iic_self hx),
        restrict‚ÇÇ]
  ¬∑ rw [IicProdIoc_le hba, ‚Üê Measure.map_map, ‚Üê Measure.fst, Measure.fst_prod]
    ¬∑ exact isProjectiveMeasureFamily_pi Œº (Iic a) (Iic b) (Iic_subset_Iic.2 hba) |>.symm
    all_goals fun_prop

/-- Let `Œº (i + 1) : Measure (X (i + 1))` be a measure. Up to an equivalence,
`Œº i = ‚®Ç j : Ioc i (i + 1), Œº i`, where `‚äó` denotes the product of measures. -/
lemma map_piSingleton (Œº : (n : ‚Ñï) ‚Üí Measure (X n)) [‚àÄ n, SigmaFinite (Œº n)] (n : ‚Ñï) :
    (Œº (n + 1)).map (piSingleton n) = Measure.pi (fun i : Ioc n (n + 1) ‚Ü¶ Œº i) := by
  refine (Measure.pi_eq fun s hs ‚Ü¶ ?_).symm
  have : Subsingleton (Ioc n (n + 1)) := by rw [Nat.Ioc_succ_singleton]; infer_instance
  rw [Fintype.prod_subsingleton _ ‚ü®n + 1, mem_Ioc.2 (by omega)‚ü©,
    Measure.map_apply (by fun_prop) (.univ_pi hs)]
  congr with x
  simp only [Set.mem_preimage, Set.mem_pi, Set.mem_univ, forall_const, Subtype.forall,
    Nat.Ioc_succ_singleton, mem_singleton]
  exact ‚ü®fun h ‚Ü¶ h (n + 1) rfl, fun h a b ‚Ü¶ b.symm ‚ñ∏ h‚ü©

end Measure

/-- `partialTraj Œ∫ a b` is a kernel which up to an equivalence is equal to
`Kernel.id √ó‚Çñ (Œ∫ a ‚äó‚Çñ ... ‚äó‚Çñ Œ∫ (b - 1))`. This lemma therefore states that if the kernels `Œ∫`
are constant then their composition-product is the product measure. -/
theorem partialTraj_const_restrict‚ÇÇ {a b : ‚Ñï} :
    (partialTraj (fun n ‚Ü¶ const _ (Œº (n + 1))) a b).map (restrict‚ÇÇ Ioc_subset_Iic_self) =
    const _ (Measure.pi (fun i : Ioc a b ‚Ü¶ Œº i)) := by
  obtain hab | hba := lt_or_ge a b
  ¬∑ refine Nat.le_induction ?_ (fun n hn hind ‚Ü¶ ?_) b (Nat.succ_le.2 hab) <;> ext1 x‚ÇÄ
    ¬∑ rw [partialTraj_succ_self, ‚Üê map_comp_right, map_apply, prod_apply, map_apply, const_apply,
        const_apply, Measure.map_piSingleton, restrict‚ÇÇ_comp_IicProdIoc, Measure.map_snd_prod,
        measure_univ, one_smul]
      all_goals fun_prop
    ¬∑ have : (restrict‚ÇÇ (Ioc_subset_Iic_self (a := a))) ‚àò (IicProdIoc (X := X) n (n + 1)) =
          (IocProdIoc a n (n + 1)) ‚àò (Prod.map (restrict‚ÇÇ Ioc_subset_Iic_self) id) := rfl
      rw [const_apply, partialTraj_succ_of_le (by omega), map_const, prod_const_comp, id_comp,
        ‚Üê map_comp_right, this, map_comp_right, ‚Üê map_prod_map, hind, Kernel.map_id, map_apply,
        prod_apply, const_apply, const_apply, Measure.map_piSingleton,
        Measure.pi_prod_map_IocProdIoc]
      any_goals fun_prop
      all_goals omega
  ¬∑ have : IsEmpty (Ioc a b) := by simpa [hba] using Subtype.isEmpty_false
    ext x s ms
    by_cases hs : s.Nonempty
    ¬∑ rw [Subsingleton.eq_univ_of_nonempty hs, @measure_univ .., measure_univ]
      exact (IsMarkovKernel.map _ (measurable_restrict‚ÇÇ _)) |>.isProbabilityMeasure x
    ¬∑ rw [Set.not_nonempty_iff_eq_empty.1 hs]
      simp

/-- `partialTraj Œ∫ a b` is a kernel which up to an equivalence is equal to
`Kernel.id √ó‚Çñ (Œ∫ a ‚äó‚Çñ ... ‚äó‚Çñ Œ∫ (b - 1))`. This lemma therefore states that if the kernel `Œ∫ i`
is constant equal to `Œº i` for all `i`, then up to an equivalence
`partialTraj Œ∫ a b = Kernel.id √ó‚Çñ Kernel.const (‚®Ç Œº i)`. -/
theorem partialTraj_const {a b : ‚Ñï} :
    partialTraj (fun n ‚Ü¶ const _ (Œº (n + 1))) a b =
      (Kernel.id √ó‚Çñ (const _ (Measure.pi (fun i : Ioc a b ‚Ü¶ Œº i)))).map (IicProdIoc a b) := by
  rw [partialTraj_eq_prod, partialTraj_const_restrict‚ÇÇ]

namespace Measure

theorem isProjectiveLimit_infinitePiNat :
    IsProjectiveLimit (infinitePiNat Œº) (fun I : Finset ‚Ñï ‚Ü¶ (Measure.pi (fun i : I ‚Ü¶ Œº i))) := by
  intro I
  rw [isProjectiveMeasureFamily_pi Œº _ _ I.subset_Iic_sup_id,
    ‚Üê restrict‚ÇÇ_comp_restrict I.subset_Iic_sup_id, ‚Üê map_map, ‚Üê frestrictLe, infinitePiNat,
    map_comp, traj_map_frestrictLe, partialTraj_const, ‚Üê map_comp, ‚Üê compProd_eq_comp_prod,
    compProd_const, pi_prod_map_IicProdIoc]
  all_goals fun_prop

/-- Restricting the product measure to a product indexed by a finset yields the usual
product measure. -/
lemma infinitePiNat_map_restrict (I : Finset ‚Ñï) :
    (infinitePiNat Œº).map I.restrict = Measure.pi fun i : I ‚Ü¶ Œº i :=
  isProjectiveLimit_infinitePiNat Œº I

theorem piContent_eq_infinitePiNat {A : Set (Œ† n, X n)} (hA : A ‚àà measurableCylinders X) :
    piContent Œº A = infinitePiNat Œº A := by
  obtain ‚ü®s, S, mS, rfl‚ü© : ‚àÉ s S, MeasurableSet S ‚àß A = cylinder s S := by
    simpa [mem_measurableCylinders] using hA
  rw [piContent_cylinder _ mS, cylinder, ‚Üê map_apply (measurable_restrict _) mS,
    infinitePiNat_map_restrict]

end Measure

end Nat

section InfinitePi

open Measure

/-! ### Product of infinitely many probability measures -/

variable {Œπ : Type*} {X : Œπ ‚Üí Type*} {mX : ‚àÄ i, MeasurableSpace (X i)}
  (Œº : (i : Œπ) ‚Üí Measure (X i)) [hŒº : ‚àÄ i, IsProbabilityMeasure (Œº i)]

/-- If we push the product measure forward by a reindexing equivalence, we get a product measure
on the reindexed product in the sense that it coincides with `piContent Œº` over
measurable cylinders. See `infinitePi_map_piCongrLeft` for a general version. -/
lemma Measure.infinitePiNat_map_piCongrLeft (e : ‚Ñï ‚âÉ Œπ) {s : Set (Œ† i, X i)}
    (hs : s ‚àà measurableCylinders X) :
    (infinitePiNat (fun n ‚Ü¶ Œº (e n))).map (piCongrLeft X e) s = piContent Œº s := by
  obtain ‚ü®I, S, hS, rfl‚ü© := (mem_measurableCylinders s).1 hs
  rw [map_apply _ hS.cylinder, cylinder, ‚Üê Set.preimage_comp, coe_piCongrLeft,
    restrict_comp_piCongrLeft, Set.preimage_comp, ‚Üê map_apply,
    infinitePiNat_map_restrict (fun n ‚Ü¶ Œº (e n)), ‚Üê cylinder, piContent_cylinder Œº hS,
    ‚Üê pi_map_piCongrLeft (e.restrictPreimageFinset I), map_apply _ hS, coe_piCongrLeft]
  ¬∑ simp
  any_goals fun_prop
  exact hS.preimage (by fun_prop)

/-- This is the key theorem to build the product of an arbitrary family of probability measures:
the `piContent` of a decreasing sequence of cylinders with empty intersection converges to `0`.

This implies the `œÉ`-additivity of `piContent` (see `addContent_iUnion_eq_sum_of_tendsto_zero`),
which allows to extend it to the `œÉ`-algebra by Carath√©odory's theorem. -/
theorem piContent_tendsto_zero {A : ‚Ñï ‚Üí Set (Œ† i, X i)} (A_mem : ‚àÄ n, A n ‚àà measurableCylinders X)
    (A_anti : Antitone A) (A_inter : ‚ãÇ n, A n = ‚àÖ) :
    Tendsto (fun n ‚Ü¶ piContent Œº (A n)) atTop (ùìù 0) := by
  have : ‚àÄ i, Nonempty (X i) := fun i ‚Ü¶ ProbabilityMeasure.nonempty ‚ü®Œº i, hŒº i‚ü©
  have A_cyl n : ‚àÉ s S, MeasurableSet S ‚àß A n = cylinder s S :=
    (mem_measurableCylinders _).1 (A_mem n)
  choose s S mS A_eq using A_cyl
  -- The family `(A‚Çô)` only depends on a countable set of coordinates, called `u`. Therefore our
  -- goal is to see it as a family indexed by this countable set, because on the product indexed
  -- by this countable set we can build a measure. To do so we have to pull back our cylinders
  -- along the injection from `Œ† i : u, X i` to `Œ† i, X i`.
  let u := ‚ãÉ n, (s n).toSet
  -- `t‚Çô` will be `s‚Çô` seen as a subset of `u`.
  let t n : Finset u := (s n).preimage Subtype.val Subtype.val_injective.injOn
  classical
  -- The map `f` allows to pull back `A‚Çô`
  let f : (Œ† i : u, X i) ‚Üí Œ† i, X i :=
    fun x i ‚Ü¶ if hi : i ‚àà u then x ‚ü®i, hi‚ü© else Classical.ofNonempty
  -- `aux` is the obvious equivalence between `s‚Çô` and `t‚Çô`
  let aux n : t n ‚âÉ s n :=
    { toFun := fun i ‚Ü¶ ‚ü®i.1.1, mem_preimage.1 i.2‚ü©
      invFun := fun i ‚Ü¶ ‚ü®‚ü®i.1, Set.mem_iUnion.2 ‚ü®n, i.2‚ü©‚ü©, mem_preimage.2 i.2‚ü©
      left_inv := fun i ‚Ü¶ by simp
      right_inv := fun i ‚Ü¶ by simp }
  -- Finally `g‚Çô` is the equivalence between the product indexed by `t‚Çô` and the one indexed by `s‚Çô`
  let g n := (aux n).piCongrLeft (fun i : s n ‚Ü¶ X i)
  -- Mapping from the product indexed by `u` by `f` and then restricting to `s‚Çô` is the same as
  -- first restricting to `t‚Çô` and then mapping by `g‚Çô`
  have r_comp_f n : (s n).restrict ‚àò f = (g n) ‚àò (fun (x : Œ† i : u, X i) i ‚Ü¶ x i) := by
    ext x i
    simp only [Function.comp_apply, Finset.restrict,
      Equiv.piCongrLeft_apply, Equiv.coe_fn_symm_mk, f, aux, g, t]
    rw [dif_pos (Set.mem_iUnion.2 ‚ü®n, i.2‚ü©)]
  -- `B‚Çô` is the same as `A‚Çô` but in the product indexed by `u`
  let B n := f ‚Åª¬π' (A n)
  -- `T‚Çô` is the same as `S‚Çô` but in the product indexed by `u`
  let T n := (g n) ‚Åª¬π' (S n)
  -- We now transfer the properties of `A‚Çô` and `S‚Çô` to `B‚Çô` and `T‚Çô`
  have B_eq n : B n = cylinder (t n) (T n) := by
    simp_rw [B, A_eq, cylinder, ‚Üê Set.preimage_comp, r_comp_f]; rfl
  have mT n : MeasurableSet (T n) := (mS n).preimage (by fun_prop)
  have B_mem n : B n ‚àà measurableCylinders (fun i : u ‚Ü¶ X i) :=
    (mem_measurableCylinders (B n)).2 ‚ü®t n, T n, mT n, B_eq n‚ü©
  have mB n : MeasurableSet (B n) := .of_mem_measurableCylinders (B_mem n)
  have B_anti : Antitone B := fun m n hmn ‚Ü¶ Set.preimage_mono <| A_anti hmn
  have B_inter : ‚ãÇ n, B n = ‚àÖ := by
    simp_rw [B, ‚Üê Set.preimage_iInter, A_inter, Set.preimage_empty]
  -- We now rewrite `piContent Œº (A n)` as `piContent (fun i : u ‚Ü¶ Œº i) (B n)`. Then there are two
  -- cases: either `u` is finite and we rewrite it to the finite product measure, either
  -- it is countable and we rewrite it to the pushforward measure of `infinitePiNat`. In both cases
  -- we have an actual measure and we can conclude with `tendsto_measure_iInter_atTop`.
  conv =>
    enter [1]; ext n
    rw [A_eq, piContent_cylinder Œº (mS n), ‚Üê pi_map_piCongrLeft (aux n),
      map_apply (by fun_prop) (mS n)]
    change (Measure.pi (fun i : t n ‚Ü¶ Œº i)) (T n)
    rw [‚Üê piContent_cylinder (fun i : u ‚Ü¶ Œº i) (mT n), ‚Üê B_eq n]
  obtain u_fin | u_inf := finite_or_infinite u
  ¬∑ let _ := Fintype.ofFinite u
    simp_rw [fun n ‚Ü¶ piContent_eq_measure_pi (fun i : u ‚Ü¶ Œº i) (mB n)]
    convert tendsto_measure_iInter_atTop (fun n ‚Ü¶ (mB n).nullMeasurableSet) B_anti
      ‚ü®0, measure_ne_top _ _‚ü©
    ¬∑ rw [B_inter, measure_empty]
    ¬∑ infer_instance
  ¬∑ -- If `u` is infinite, then we have an equivalence with `‚Ñï` so we can apply `secondLemma`.
    have count_u : Countable u := Set.countable_iUnion (fun n ‚Ü¶ (s n).countable_toSet)
    obtain ‚ü®œÜ, -‚ü© := Classical.exists_true_of_nonempty (Œ± := ‚Ñï ‚âÉ u) nonempty_equiv_of_countable
    conv => enter [1]; ext n; rw [‚Üê infinitePiNat_map_piCongrLeft _ œÜ (B_mem n)]
    convert tendsto_measure_iInter_atTop (fun n ‚Ü¶ (mB n).nullMeasurableSet) B_anti
      ‚ü®0, measure_ne_top _ _‚ü©
    ¬∑ rw [B_inter, measure_empty]
    ¬∑ infer_instance

/-- The `projectiveFamilyContent` associated to a family of probability measures is
œÉ-subadditive. -/
theorem isSigmaSubadditive_piContent : (piContent Œº).IsSigmaSubadditive := by
  refine isSigmaSubadditive_of_addContent_iUnion_eq_tsum
    isSetRing_measurableCylinders (fun f hf hf_Union hf' ‚Ü¶ ?_)
  exact addContent_iUnion_eq_sum_of_tendsto_zero isSetRing_measurableCylinders
    (piContent Œº) (fun s hs ‚Ü¶ projectiveFamilyContent_ne_top _)
    (fun _ ‚Ü¶ piContent_tendsto_zero Œº) hf hf_Union hf'

namespace Measure

/-- The product measure of an arbitrary family of probability measures. It is defined as the unique
extension of the function which gives to cylinders the measure given by the associated product
measure. -/
noncomputable def infinitePi : Measure (Œ† i, X i) :=
  (piContent Œº).measure isSetSemiring_measurableCylinders
    generateFrom_measurableCylinders.ge (isSigmaSubadditive_piContent Œº)

/-- The product measure is the projective limit of the partial product measures. This ensures
uniqueness and expresses the value of the product measure applied to cylinders. -/
theorem isProjectiveLimit_infinitePi :
    IsProjectiveLimit (infinitePi Œº) (fun I : Finset Œπ ‚Ü¶ (Measure.pi (fun i : I ‚Ü¶ Œº i))) := by
  intro I
  ext s hs
  rw [map_apply (measurable_restrict I) hs, infinitePi, AddContent.measure_eq, ‚Üê cylinder,
    piContent_cylinder Œº hs]
  ¬∑ exact generateFrom_measurableCylinders.symm
  ¬∑ exact cylinder_mem_measurableCylinders _ _ hs

/-- Restricting the product measure to a product indexed by a finset yields the usual
product measure. -/
theorem infinitePi_map_restrict {I : Finset Œπ} :
    (Measure.infinitePi Œº).map I.restrict = Measure.pi fun i : I ‚Ü¶ Œº i :=
  isProjectiveLimit_infinitePi Œº I

instance : IsProbabilityMeasure (infinitePi Œº) := by
  constructor
  rw [‚Üê cylinder_univ ‚àÖ, cylinder, ‚Üê map_apply (measurable_restrict _) .univ,
    infinitePi_map_restrict, measure_univ]

/-- To prove that a measure is equal to the product measure it is enough to check that it
it gives the same measure to measurable boxes. -/
theorem eq_infinitePi {ŒΩ : Measure (Œ† i, X i)}
    (hŒΩ : ‚àÄ s : Finset Œπ, ‚àÄ t : (i : Œπ) ‚Üí Set (X i),
      (‚àÄ i ‚àà s, MeasurableSet (t i)) ‚Üí ŒΩ (Set.pi s t) = ‚àè i ‚àà s, Œº i (t i)) :
    ŒΩ = infinitePi Œº := by
  refine (isProjectiveLimit_infinitePi Œº).unique ?_ |>.symm
  refine fun s ‚Ü¶ (pi_eq fun t ht ‚Ü¶ ?_).symm
  classical
  rw [Measure.map_apply, restrict_preimage, hŒΩ, ‚Üê prod_attach, univ_eq_attach]
  ¬∑ congr with i
    rw [dif_pos i.2]
  any_goals fun_prop
  ¬∑ exact fun i hi ‚Ü¶ dif_pos hi ‚ñ∏ ht ‚ü®i, hi‚ü©
  ¬∑ exact .univ_pi ht

-- TODO: add a version for an infinite product
lemma infinitePi_pi {s : Finset Œπ} {t : (i : Œπ) ‚Üí Set (X i)}
    (mt : ‚àÄ i ‚àà s, MeasurableSet (t i)) :
    infinitePi Œº (Set.pi s t) = ‚àè i ‚àà s, (Œº i) (t i) := by
  have : Set.pi s t = cylinder s ((@Set.univ s).pi (fun i : s ‚Ü¶ t i)) := by
    ext x
    simp
  rw [this, cylinder, ‚Üê map_apply, infinitePi_map_restrict, pi_pi]
  ¬∑ rw [univ_eq_attach, prod_attach _ (fun i ‚Ü¶ (Œº i) (t i))]
  ¬∑ exact measurable_restrict _
  ¬∑ exact .univ_pi fun i ‚Ü¶ mt i.1 i.2

lemma _root_.measurePreserving_eval_infinitePi (i : Œπ) :
    MeasurePreserving (Function.eval i) (infinitePi Œº) (Œº i) where
  measurable := by fun_prop
  map_eq := by
    ext s hs
    have : @Function.eval Œπ X i =
        (@Function.eval ({i} : Finset Œπ) (fun j ‚Ü¶ X j) ‚ü®i, by simp‚ü©) ‚àò
        (Finset.restrict {i}) := by ext; simp
    rw [this, ‚Üê map_map, infinitePi_map_restrict, (measurePreserving_eval _ _).map_eq]
    all_goals fun_prop

lemma infinitePi_map_pi {Y : Œπ ‚Üí Type*} [‚àÄ i, MeasurableSpace (Y i)] {f : (i : Œπ) ‚Üí X i ‚Üí Y i}
    (hf : ‚àÄ i, Measurable (f i)) :
    haveI (i : Œπ) : IsProbabilityMeasure ((Œº i).map (f i)) :=
      isProbabilityMeasure_map (hf i).aemeasurable
    (infinitePi Œº).map (fun x i ‚Ü¶ f i (x i)) = infinitePi (fun i ‚Ü¶ (Œº i).map (f i)) := by
  have (i : Œπ) : IsProbabilityMeasure ((Œº i).map (f i)) :=
    isProbabilityMeasure_map (hf i).aemeasurable
  refine eq_infinitePi _ fun s t ht ‚Ü¶ ?_
  rw [map_apply (by fun_prop) (.pi s.countable_toSet ht)]
  have : (fun (x : Œ† i, X i) i ‚Ü¶ f i (x i)) ‚Åª¬π' ((s : Set Œπ).pi t) =
      (s : Set Œπ).pi (fun i ‚Ü¶ (f i) ‚Åª¬π' (t i)) := by ext x; simp
  rw [this, infinitePi_pi _ (fun i hi ‚Ü¶ hf i (ht i hi))]
  refine Finset.prod_congr rfl fun i hi ‚Ü¶ ?_
  rw [map_apply (by fun_prop) (ht i hi)]

/-- If we push the product measure forward by a reindexing equivalence, we get a product measure
on the reindexed product. -/
theorem infinitePi_map_piCongrLeft {Œ± : Type*} (e : Œ± ‚âÉ Œπ) :
    (infinitePi (fun i ‚Ü¶ Œº (e i))).map (piCongrLeft X e) = infinitePi Œº := by
  refine eq_infinitePi Œº fun s t ht ‚Ü¶ ?_
  conv_lhs => enter [2, 1]; rw [‚Üê e.image_preimage s, ‚Üê coe_preimage _ e.injective.injOn]
  rw [map_apply, coe_piCongrLeft, Equiv.piCongrLeft_preimage_pi, infinitePi_pi,
    prod_equiv e]
  ¬∑ simp
  ¬∑ simp
  ¬∑ simp_all
  ¬∑ fun_prop
  ¬∑ exact .pi ((countable_toSet _).image e) (by simp_all)

theorem infinitePi_eq_pi [Fintype Œπ] : infinitePi Œº = Measure.pi Œº := by
  refine (pi_eq fun s hs ‚Ü¶ ?_).symm
  rw [‚Üê coe_univ, infinitePi_pi]
  simpa

lemma infinitePi_cylinder {s : Finset Œπ} {S : Set (Œ† i : s, X i)} (mS : MeasurableSet S) :
    infinitePi Œº (cylinder s S) = Measure.pi (fun i : s ‚Ü¶ Œº i) S := by
  rw [cylinder, ‚Üê Measure.map_apply (measurable_restrict _) mS, infinitePi_map_restrict]

end Measure

section Integral

theorem integral_restrict_infinitePi {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
    {s : Finset Œπ} {f : (Œ† i : s, X i) ‚Üí E}
    (hf : AEStronglyMeasurable f (Measure.pi (fun i : s ‚Ü¶ Œº i))) :
    ‚à´ y, f (s.restrict y) ‚àÇinfinitePi Œº = ‚à´ y, f y ‚àÇMeasure.pi (fun i : s ‚Ü¶ Œº i) := by
  rw [‚Üê integral_map, infinitePi_map_restrict]
  ¬∑ fun_prop
  ¬∑ rwa [infinitePi_map_restrict]

theorem lintegral_restrict_infinitePi {s : Finset Œπ}
    {f : (Œ† i : s, X i) ‚Üí ‚Ñù‚â•0‚àû} (hf : Measurable f) :
    ‚à´‚Åª y, f (s.restrict y) ‚àÇinfinitePi Œº = ‚à´‚Åª y, f y ‚àÇMeasure.pi (fun i : s ‚Ü¶ Œº i) := by
  rw [‚Üê lintegral_map hf (measurable_restrict _), isProjectiveLimit_infinitePi Œº]

open Filtration

theorem integral_infinitePi_of_piFinset [DecidableEq Œπ] {E : Type*} [NormedAddCommGroup E]
    [NormedSpace ‚Ñù E] {s : Finset Œπ} {f : (Œ† i, X i) ‚Üí E}
    (mf : StronglyMeasurable[piFinset s] f) (x : Œ† i, X i) :
    ‚à´ y, f y ‚àÇinfinitePi Œº =
    ‚à´ y, f (Function.updateFinset x s y) ‚àÇMeasure.pi (fun i : s ‚Ü¶ Œº i) := by
  let g : (Œ† i : s, X i) ‚Üí E := fun y ‚Ü¶ f (Function.updateFinset x _ y)
  have this y : g (s.restrict y) = f y :=
    mf.dependsOn_of_piFinset fun i hi ‚Ü¶ by simp_all [Function.updateFinset]
  rw [‚Üê integral_congr_ae <| ae_of_all _ this, integral_restrict_infinitePi]
  exact mf.comp_measurable (measurable_updateFinset.mono le_rfl (piFinset.le s))
    |>.aestronglyMeasurable

theorem lintegral_infinitePi_of_piFinset [DecidableEq Œπ] {s : Finset Œπ}
    {f : (Œ† i, X i) ‚Üí ‚Ñù‚â•0‚àû} (mf : Measurable[piFinset s] f)
    (x : Œ† i, X i) : ‚à´‚Åª y, f y ‚àÇinfinitePi Œº = (‚à´‚ãØ‚à´‚Åª_s, f ‚àÇŒº) x := by
  let g : (Œ† i : s, X i) ‚Üí ‚Ñù‚â•0‚àû := fun y ‚Ü¶ f (Function.updateFinset x _ y)
  have this y : g (s.restrict y) = f y :=
    mf.dependsOn_of_piFinset fun i hi ‚Ü¶ by simp_all [Function.updateFinset]
  rw [‚Üê lintegral_congr_ae <| ae_of_all _ this, lintegral_restrict_infinitePi]
  ¬∑ rfl
  ¬∑ exact mf.comp (measurable_updateFinset.mono le_rfl (piFinset.le s))

end Integral

end InfinitePi

end MeasureTheory
