/-
Copyright (c) 2025 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.MeasureTheory.Function.ConditionalExpectation.Real
import Mathlib.MeasureTheory.Integral.Average
import Mathlib.Probability.Moments.Variance

/-!
# Conditional variance

This file defines the variance of a real-valued random variable conditional to a sigma-algebra.

## TODO

Define the Lebesgue conditional variance. See
[GibbsMeasure](https://github.com/james18lpc/GibbsMeasure) for a definition of the Lebesgue
conditional expectation).
-/

open MeasureTheory Filter
open scoped ENNReal

namespace ProbabilityTheory
variable {Î© : Type*} {mâ‚€ m m' : MeasurableSpace Î©} {hm : m â‰¤ mâ‚€} {X Y : Î© â†’ â„} {Î¼ : Measure[mâ‚€] Î©}
  {s : Set Î©}

variable (m X Î¼) in
/-- Conditional variance of a real-valued random variable. It is defined as `0` if any one of the
following conditions is true:
- `m` is not a sub-Ïƒ-algebra of `mâ‚€`,
- `Î¼` is not Ïƒ-finite with respect to `m`,
- `X - Î¼[X | m]` is not square-integrable. -/
noncomputable def condVar : Î© â†’ â„ := Î¼[(X - Î¼[X | m]) ^ 2 | m]

@[inherit_doc] scoped notation "Var[" X "; " Î¼ " | " m "]" => condVar m X Î¼

/-- Conditional variance of a real-valued random variable. It is defined as `0` if any one of the
following conditions is true:
- `m` is not a sub-Ïƒ-algebra of `mâ‚€`,
- `volume` is not Ïƒ-finite with respect to `m`,
- `X - ğ”¼[X | m]` is not square-integrable. -/
scoped notation "Var[" f "|" m "]" => Var[f; MeasureTheory.volume | m]

lemma condVar_of_not_le (hm : Â¬m â‰¤ mâ‚€) : Var[X; Î¼ | m] = 0 := by rw [condVar, condExp_of_not_le hm]

lemma condVar_of_not_sigmaFinite (hÎ¼m : Â¬SigmaFinite (Î¼.trim hm)) :
    Var[X; Î¼ | m] = 0 := by rw [condVar, condExp_of_not_sigmaFinite hm hÎ¼m]

open scoped Classical in
lemma condVar_of_sigmaFinite [SigmaFinite (Î¼.trim hm)] :
    Var[X; Î¼ | m] =
      if Integrable (fun Ï‰ â†¦ (X Ï‰ - (Î¼[X | m]) Ï‰) ^ 2) Î¼ then
        if StronglyMeasurable[m] (fun Ï‰ â†¦ (X Ï‰ - (Î¼[X | m]) Ï‰) ^ 2) then
          fun Ï‰ â†¦ (X Ï‰ - (Î¼[X | m]) Ï‰) ^ 2
        else aestronglyMeasurable_condExpL1.mk (condExpL1 hm Î¼ fun Ï‰ â†¦ (X Ï‰ - (Î¼[X | m]) Ï‰) ^ 2)
      else 0 := condExp_of_sigmaFinite _

lemma condVar_of_stronglyMeasurable [SigmaFinite (Î¼.trim hm)]
    (hX : StronglyMeasurable[m] X) (hXint : Integrable ((X - Î¼[X|m]) ^ 2) Î¼) :
    Var[X; Î¼ | m] = fun Ï‰ â†¦ (X Ï‰ - (Î¼[X | m]) Ï‰) ^ 2 :=
  condExp_of_stronglyMeasurable _ ((hX.sub stronglyMeasurable_condExp).pow _) hXint

lemma condVar_of_not_integrable (hXint : Â¬ Integrable (fun Ï‰ â†¦ (X Ï‰ - (Î¼[X | m]) Ï‰) ^ 2) Î¼) :
    Var[X; Î¼ | m] = 0 := condExp_of_not_integrable hXint

@[simp] lemma condVar_zero : Var[0; Î¼ | m] = 0 := by simp [condVar]

@[simp]
lemma condVar_const (hm : m â‰¤ mâ‚€) (c : â„) : Var[fun _ â†¦ c; Î¼ | m] = 0 := by
  obtain rfl | hc := eq_or_ne c 0
  Â· simp [â† Pi.zero_def]
  by_cases hÎ¼m : IsFiniteMeasure Î¼
  Â· simp [condVar, hm]
  Â· simp [condVar, condExp_of_not_integrable, integrable_const_iff_isFiniteMeasure hc,
      integrable_const_iff_isFiniteMeasure <| pow_ne_zero _ hc, hÎ¼m, Pi.pow_def]

lemma stronglyMeasurable_condVar : StronglyMeasurable[m] (Var[X; Î¼ | m]) :=
  stronglyMeasurable_condExp

lemma condVar_congr_ae (h : X =áµ[Î¼] Y) : Var[X; Î¼ | m] =áµ[Î¼] Var[Y; Î¼ | m] :=
  condExp_congr_ae <| by filter_upwards [h, condExp_congr_ae h] with Ï‰ hÏ‰ hÏ‰'; dsimp; rw [hÏ‰, hÏ‰']

lemma condVar_of_aestronglyMeasurable [hÎ¼m : SigmaFinite (Î¼.trim hm)]
    (hX : AEStronglyMeasurable[m] X Î¼) (hXint : Integrable ((X - Î¼[X|m]) ^ 2) Î¼) :
    Var[X; Î¼ | m] =áµ[Î¼] (X - Î¼[X | m]) ^ 2 :=
  condExp_of_aestronglyMeasurable' _ ((continuous_pow _).comp_aestronglyMeasurable
    (hX.sub stronglyMeasurable_condExp.aestronglyMeasurable)) hXint

lemma integrable_condVar : Integrable Var[X; Î¼ | m] Î¼ := integrable_condExp

/-- The integral of the conditional variance `Var[X | m]` over an `m`-measurable set is equal to
the integral of `(X - Î¼[X | m]) ^ 2` on that set. -/
lemma setIntegral_condVar [SigmaFinite (Î¼.trim hm)] (hX : Integrable ((X - Î¼[X|m]) ^ 2) Î¼)
    (hs : MeasurableSet[m] s) :
    âˆ« Ï‰ in s, (Var[X; Î¼ | m]) Ï‰ âˆ‚Î¼ = âˆ« Ï‰ in s, (X Ï‰ - (Î¼[X | m]) Ï‰) ^ 2 âˆ‚Î¼ :=
  setIntegral_condExp _ hX hs

-- `(Â· ^ 2)` is a postfix operator called `_sq` in lemma names, but
-- `condVar_ae_eq_condExp_sq_sub_condExp_sq` is a bit ridiculous, so we exceptionally denote it by
-- `sq_` as it were a prefix.
lemma condVar_ae_eq_condExp_sq_sub_sq_condExp (hm : m â‰¤ mâ‚€) [IsFiniteMeasure Î¼] (hX : MemLp X 2 Î¼) :
    Var[X; Î¼ | m] =áµ[Î¼] Î¼[X ^ 2 | m] - Î¼[X | m] ^ 2 := by
  calc
    Var[X; Î¼ | m]
    _ = Î¼[X ^ 2 - 2 * X * Î¼[X | m] + Î¼[X | m] ^ 2 | m] := by rw [condVar, sub_sq]
    _ =áµ[Î¼] Î¼[X ^ 2 | m] - 2 * Î¼[X | m] ^ 2 + Î¼[X | m] ^ 2 := by
      have auxâ‚€ : Integrable (X ^ 2) Î¼ := hX.integrable_sq
      have auxâ‚ : Integrable (2 * X * Î¼[X | m]) Î¼ := by
        rw [mul_assoc]
        exact (memLp_one_iff_integrable.1 <| hX.condExp.mul hX).const_mul _
      have auxâ‚‚ : Integrable (Î¼[X | m] ^ 2) Î¼ := hX.condExp.integrable_sq
      filter_upwards [condExp_add (m := m) (auxâ‚€.sub auxâ‚) auxâ‚‚, condExp_sub (m := m) auxâ‚€ auxâ‚,
        condExp_mul_of_stronglyMeasurable_right stronglyMeasurable_condExp auxâ‚
          ((hX.integrable one_le_two).const_mul _), condExp_ofNat (m := m) 2 X]
        with Ï‰ hÏ‰â‚€ hÏ‰â‚ hÏ‰â‚‚ hÏ‰â‚ƒ
      simp [hÏ‰â‚€, hÏ‰â‚, hÏ‰â‚‚, hÏ‰â‚ƒ,
        condExp_of_stronglyMeasurable hm (stronglyMeasurable_condExp.pow _) auxâ‚‚]
      simp [mul_assoc, sq]
    _ = Î¼[X ^ 2 | m] - Î¼[X | m] ^ 2 := by ring

lemma condVar_ae_le_condExp_sq (hm : m â‰¤ mâ‚€) [IsFiniteMeasure Î¼] (hX : MemLp X 2 Î¼) :
    Var[X; Î¼ | m] â‰¤áµ[Î¼] Î¼[X ^ 2 | m] := by
  filter_upwards [condVar_ae_eq_condExp_sq_sub_sq_condExp hm hX] with Ï‰ hÏ‰
  dsimp at hÏ‰
  nlinarith

/-- **Law of total variance** -/
lemma integral_condVar_add_variance_condExp (hm : m â‰¤ mâ‚€) [IsProbabilityMeasure Î¼]
    (hX : MemLp X 2 Î¼) : Î¼[Var[X; Î¼ | m]] + Var[Î¼[X | m]; Î¼] = Var[X; Î¼] := by
  calc
    Î¼[Var[X; Î¼ | m]] + Var[Î¼[X | m]; Î¼]
    _ = Î¼[(Î¼[X ^ 2 | m] - Î¼[X | m] ^ 2 : Î© â†’ â„)] + (Î¼[Î¼[X | m] ^ 2] - Î¼[Î¼[X | m]] ^ 2) := by
      congr 1
      Â· exact integral_congr_ae <| condVar_ae_eq_condExp_sq_sub_sq_condExp hm hX
      Â· exact variance_eq_sub hX.condExp
    _ = Î¼[X ^ 2] - Î¼[Î¼[X | m] ^ 2] + (Î¼[Î¼[X | m] ^ 2] - Î¼[X] ^ 2) := by
      rw [integral_sub' integrable_condExp, integral_condExp hm, integral_condExp hm]
      exact hX.condExp.integrable_sq
    _ = Var[X; Î¼] := by rw [variance_eq_sub hX]; ring

lemma condVar_bot' [NeZero Î¼] (X : Î© â†’ â„) :
    Var[X; Î¼ | âŠ¥] = fun _ => â¨ Ï‰, (X Ï‰ - â¨ Ï‰', X Ï‰' âˆ‚Î¼) ^ 2 âˆ‚Î¼ := by
  ext Ï‰; simp [condVar, condExp_bot', average, measureReal_def]

lemma condVar_bot_ae_eq (X : Î© â†’ â„) :
    Var[X; Î¼ | âŠ¥] =áµ[Î¼] fun _ â†¦ â¨ Ï‰, (X Ï‰ - â¨ Ï‰', X Ï‰' âˆ‚Î¼) ^ 2 âˆ‚Î¼ := by
  obtain rfl | hÎ¼ := eq_zero_or_neZero Î¼
  Â· rw [ae_zero]
    exact eventually_bot
  Â· exact .of_forall <| congr_fun (condVar_bot' X)

lemma condVar_bot [IsProbabilityMeasure Î¼] (hX : AEMeasurable X Î¼) :
    Var[X; Î¼ | âŠ¥] = fun _Ï‰ â†¦ Var[X; Î¼] := by
  simp [condVar_bot', average_eq_integral, variance_eq_integral hX]

lemma condVar_smul (c : â„) (X : Î© â†’ â„) : Var[c â€¢ X; Î¼ | m] =áµ[Î¼] c ^ 2 â€¢ Var[X; Î¼ | m] := by
  calc
    Var[c â€¢ X; Î¼ | m]
    _ =áµ[Î¼] Î¼[c ^ 2 â€¢ (X - Î¼[X | m]) ^ 2 | m] := by
      rw [condVar]
      refine condExp_congr_ae ?_
      filter_upwards [condExp_smul (m := m) c X] with Ï‰ hÏ‰
      simp [hÏ‰, â† mul_sub, mul_pow]
    _ =áµ[Î¼] c ^ 2 â€¢ Var[X; Î¼ | m] := condExp_smul ..

@[simp] lemma condVar_neg (X : Î© â†’ â„) : Var[-X; Î¼ | m] =áµ[Î¼] Var[X; Î¼ | m] := by
  refine condExp_congr_ae ?_
  filter_upwards [condExp_neg (m := m) X] with Ï‰ hÏ‰
  simp [hÏ‰]
  ring

end ProbabilityTheory
