/-
Copyright (c) 2021 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying, RÃ©my Degenne
-/
import Mathlib.Probability.Process.Adapted
import Mathlib.MeasureTheory.Constructions.BorelSpace.Order

/-!
# Stopping times, stopped processes and stopped values

Definition and properties of stopping times.

## Main definitions

* `MeasureTheory.IsStoppingTime`: a stopping time with respect to some filtration `f` is a
  function `Ï„` such that for all `i`, the preimage of `{j | j â‰¤ i}` along `Ï„` is
  `f i`-measurable
* `MeasureTheory.IsStoppingTime.measurableSpace`: the Ïƒ-algebra associated with a stopping time

## Main results

* `ProgMeasurable.stoppedProcess`: the stopped process of a progressively measurable process is
  progressively measurable.
* `memLp_stoppedProcess`: if a process belongs to `â„’p` at every time in `â„•`, then its stopped
  process belongs to `â„’p` as well.

## Tags

stopping time, stochastic process

-/

open Filter Order TopologicalSpace

open scoped MeasureTheory NNReal ENNReal Topology

namespace MeasureTheory

variable {Î© Î² Î¹ : Type*} {m : MeasurableSpace Î©}

/-! ### Stopping times -/


/-- A stopping time with respect to some filtration `f` is a function
`Ï„` such that for all `i`, the preimage of `{j | j â‰¤ i}` along `Ï„` is measurable
with respect to `f i`.

Intuitively, the stopping time `Ï„` describes some stopping rule such that at time
`i`, we may determine it with the information we have at time `i`. -/
def IsStoppingTime [Preorder Î¹] (f : Filtration Î¹ m) (Ï„ : Î© â†’ Î¹) :=
  âˆ€ i : Î¹, MeasurableSet[f i] <| {Ï‰ | Ï„ Ï‰ â‰¤ i}

theorem isStoppingTime_const [Preorder Î¹] (f : Filtration Î¹ m) (i : Î¹) :
    IsStoppingTime f fun _ => i := fun j => by simp only [MeasurableSet.const]

section MeasurableSet

section Preorder

variable [Preorder Î¹] {f : Filtration Î¹ m} {Ï„ : Î© â†’ Î¹}

protected theorem IsStoppingTime.measurableSet_le (hÏ„ : IsStoppingTime f Ï„) (i : Î¹) :
    MeasurableSet[f i] {Ï‰ | Ï„ Ï‰ â‰¤ i} :=
  hÏ„ i

theorem IsStoppingTime.measurableSet_lt_of_pred [PredOrder Î¹] (hÏ„ : IsStoppingTime f Ï„) (i : Î¹) :
    MeasurableSet[f i] {Ï‰ | Ï„ Ï‰ < i} := by
  by_cases hi_min : IsMin i
  Â· suffices {Ï‰ : Î© | Ï„ Ï‰ < i} = âˆ… by rw [this]; exact @MeasurableSet.empty _ (f i)
    ext1 Ï‰
    simp only [Set.mem_setOf_eq, Set.mem_empty_iff_false, iff_false]
    rw [isMin_iff_forall_not_lt] at hi_min
    exact hi_min (Ï„ Ï‰)
  have : {Ï‰ : Î© | Ï„ Ï‰ < i} = Ï„ â»Â¹' Set.Iic (pred i) := by ext; simp [Iic_pred_of_not_isMin hi_min]
  rw [this]
  exact f.mono (pred_le i) _ (hÏ„.measurableSet_le <| pred i)

end Preorder

section CountableStoppingTime

namespace IsStoppingTime

variable [PartialOrder Î¹] {Ï„ : Î© â†’ Î¹} {f : Filtration Î¹ m}

protected theorem measurableSet_eq_of_countable_range (hÏ„ : IsStoppingTime f Ï„)
    (h_countable : (Set.range Ï„).Countable) (i : Î¹) : MeasurableSet[f i] {Ï‰ | Ï„ Ï‰ = i} := by
  have : {Ï‰ | Ï„ Ï‰ = i} = {Ï‰ | Ï„ Ï‰ â‰¤ i} \ â‹ƒ (j âˆˆ Set.range Ï„) (_ : j < i), {Ï‰ | Ï„ Ï‰ â‰¤ j} := by
    ext1 a
    simp only [Set.mem_setOf_eq, Set.mem_range, Set.iUnion_exists, Set.iUnion_iUnion_eq',
      Set.mem_diff, Set.mem_iUnion, exists_prop, not_exists, not_and]
    constructor <;> intro h
    Â· simp only [h, lt_iff_le_not_ge, le_refl, and_imp, imp_self, imp_true_iff, and_self_iff]
    Â· exact h.1.eq_or_lt.resolve_right fun h_lt => h.2 a h_lt le_rfl
  rw [this]
  refine (hÏ„.measurableSet_le i).diff ?_
  refine MeasurableSet.biUnion h_countable fun j _ => ?_
  classical
  rw [Set.iUnion_eq_if]
  split_ifs with hji
  Â· exact f.mono hji.le _ (hÏ„.measurableSet_le j)
  Â· exact @MeasurableSet.empty _ (f i)

protected theorem measurableSet_eq_of_countable [Countable Î¹] (hÏ„ : IsStoppingTime f Ï„) (i : Î¹) :
    MeasurableSet[f i] {Ï‰ | Ï„ Ï‰ = i} :=
  hÏ„.measurableSet_eq_of_countable_range (Set.to_countable _) i

protected theorem measurableSet_lt_of_countable_range (hÏ„ : IsStoppingTime f Ï„)
    (h_countable : (Set.range Ï„).Countable) (i : Î¹) : MeasurableSet[f i] {Ï‰ | Ï„ Ï‰ < i} := by
  have : {Ï‰ | Ï„ Ï‰ < i} = {Ï‰ | Ï„ Ï‰ â‰¤ i} \ {Ï‰ | Ï„ Ï‰ = i} := by ext1 Ï‰; simp [lt_iff_le_and_ne]
  rw [this]
  exact (hÏ„.measurableSet_le i).diff (hÏ„.measurableSet_eq_of_countable_range h_countable i)

protected theorem measurableSet_lt_of_countable [Countable Î¹] (hÏ„ : IsStoppingTime f Ï„) (i : Î¹) :
    MeasurableSet[f i] {Ï‰ | Ï„ Ï‰ < i} :=
  hÏ„.measurableSet_lt_of_countable_range (Set.to_countable _) i

protected theorem measurableSet_ge_of_countable_range {Î¹} [LinearOrder Î¹] {Ï„ : Î© â†’ Î¹}
    {f : Filtration Î¹ m} (hÏ„ : IsStoppingTime f Ï„) (h_countable : (Set.range Ï„).Countable) (i : Î¹) :
    MeasurableSet[f i] {Ï‰ | i â‰¤ Ï„ Ï‰} := by
  have : {Ï‰ | i â‰¤ Ï„ Ï‰} = {Ï‰ | Ï„ Ï‰ < i}á¶œ := by
    ext1 Ï‰; simp only [Set.mem_setOf_eq, Set.mem_compl_iff, not_lt]
  rw [this]
  exact (hÏ„.measurableSet_lt_of_countable_range h_countable i).compl

protected theorem measurableSet_ge_of_countable {Î¹} [LinearOrder Î¹] {Ï„ : Î© â†’ Î¹} {f : Filtration Î¹ m}
    [Countable Î¹] (hÏ„ : IsStoppingTime f Ï„) (i : Î¹) : MeasurableSet[f i] {Ï‰ | i â‰¤ Ï„ Ï‰} :=
  hÏ„.measurableSet_ge_of_countable_range (Set.to_countable _) i

end IsStoppingTime

end CountableStoppingTime

section LinearOrder

variable [LinearOrder Î¹] {f : Filtration Î¹ m} {Ï„ : Î© â†’ Î¹}

theorem IsStoppingTime.measurableSet_gt (hÏ„ : IsStoppingTime f Ï„) (i : Î¹) :
    MeasurableSet[f i] {Ï‰ | i < Ï„ Ï‰} := by
  have : {Ï‰ | i < Ï„ Ï‰} = {Ï‰ | Ï„ Ï‰ â‰¤ i}á¶œ := by
    ext1 Ï‰; simp only [Set.mem_setOf_eq, Set.mem_compl_iff, not_le]
  rw [this]
  exact (hÏ„.measurableSet_le i).compl

section TopologicalSpace

variable [TopologicalSpace Î¹] [OrderTopology Î¹] [FirstCountableTopology Î¹]

/-- Auxiliary lemma for `MeasureTheory.IsStoppingTime.measurableSet_lt`. -/
theorem IsStoppingTime.measurableSet_lt_of_isLUB (hÏ„ : IsStoppingTime f Ï„) (i : Î¹)
    (h_lub : IsLUB (Set.Iio i) i) : MeasurableSet[f i] {Ï‰ | Ï„ Ï‰ < i} := by
  by_cases hi_min : IsMin i
  Â· suffices {Ï‰ | Ï„ Ï‰ < i} = âˆ… by rw [this]; exact @MeasurableSet.empty _ (f i)
    ext1 Ï‰
    simp only [Set.mem_setOf_eq, Set.mem_empty_iff_false, iff_false]
    exact isMin_iff_forall_not_lt.mp hi_min (Ï„ Ï‰)
  obtain âŸ¨seq, -, -, h_tendsto, h_boundâŸ© :
      âˆƒ seq : â„• â†’ Î¹, Monotone seq âˆ§ (âˆ€ j, seq j â‰¤ i) âˆ§ Tendsto seq atTop (ğ“ i) âˆ§ âˆ€ j, seq j < i :=
    h_lub.exists_seq_monotone_tendsto (not_isMin_iff.mp hi_min)
  have h_Ioi_eq_Union : Set.Iio i = â‹ƒ j, {k | k â‰¤ seq j} := by
    ext1 k
    simp only [Set.mem_Iio, Set.mem_iUnion, Set.mem_setOf_eq]
    refine âŸ¨fun hk_lt_i => ?_, fun h_exists_k_le_seq => ?_âŸ©
    Â· rw [tendsto_atTop'] at h_tendsto
      have h_nhds : Set.Ici k âˆˆ ğ“ i :=
        mem_nhds_iff.mpr âŸ¨Set.Ioi k, Set.Ioi_subset_Ici le_rfl, isOpen_Ioi, hk_lt_iâŸ©
      obtain âŸ¨a, haâŸ© : âˆƒ a : â„•, âˆ€ b : â„•, b â‰¥ a â†’ k â‰¤ seq b := h_tendsto (Set.Ici k) h_nhds
      exact âŸ¨a, ha a le_rflâŸ©
    Â· obtain âŸ¨j, hk_seq_jâŸ© := h_exists_k_le_seq
      exact hk_seq_j.trans_lt (h_bound j)
  have h_lt_eq_preimage : {Ï‰ | Ï„ Ï‰ < i} = Ï„ â»Â¹' Set.Iio i := by
    ext1 Ï‰; simp only [Set.mem_setOf_eq, Set.mem_preimage, Set.mem_Iio]
  rw [h_lt_eq_preimage, h_Ioi_eq_Union]
  simp only [Set.preimage_iUnion, Set.preimage_setOf_eq]
  exact MeasurableSet.iUnion fun n => f.mono (h_bound n).le _ (hÏ„.measurableSet_le (seq n))

theorem IsStoppingTime.measurableSet_lt (hÏ„ : IsStoppingTime f Ï„) (i : Î¹) :
    MeasurableSet[f i] {Ï‰ | Ï„ Ï‰ < i} := by
  obtain âŸ¨i', hi'_lubâŸ© : âˆƒ i', IsLUB (Set.Iio i) i' := exists_lub_Iio i
  rcases lub_Iio_eq_self_or_Iio_eq_Iic i hi'_lub with hi'_eq_i | h_Iio_eq_Iic
  Â· rw [â† hi'_eq_i] at hi'_lub âŠ¢
    exact hÏ„.measurableSet_lt_of_isLUB i' hi'_lub
  Â· have h_lt_eq_preimage : {Ï‰ : Î© | Ï„ Ï‰ < i} = Ï„ â»Â¹' Set.Iio i := rfl
    rw [h_lt_eq_preimage, h_Iio_eq_Iic]
    exact f.mono (lub_Iio_le i hi'_lub) _ (hÏ„.measurableSet_le i')

theorem IsStoppingTime.measurableSet_ge (hÏ„ : IsStoppingTime f Ï„) (i : Î¹) :
    MeasurableSet[f i] {Ï‰ | i â‰¤ Ï„ Ï‰} := by
  have : {Ï‰ | i â‰¤ Ï„ Ï‰} = {Ï‰ | Ï„ Ï‰ < i}á¶œ := by
    ext1 Ï‰; simp only [Set.mem_setOf_eq, Set.mem_compl_iff, not_lt]
  rw [this]
  exact (hÏ„.measurableSet_lt i).compl

theorem IsStoppingTime.measurableSet_eq (hÏ„ : IsStoppingTime f Ï„) (i : Î¹) :
    MeasurableSet[f i] {Ï‰ | Ï„ Ï‰ = i} := by
  have : {Ï‰ | Ï„ Ï‰ = i} = {Ï‰ | Ï„ Ï‰ â‰¤ i} âˆ© {Ï‰ | Ï„ Ï‰ â‰¥ i} := by
    ext1 Ï‰; simp only [Set.mem_setOf_eq, Set.mem_inter_iff, le_antisymm_iff]
  rw [this]
  exact (hÏ„.measurableSet_le i).inter (hÏ„.measurableSet_ge i)

theorem IsStoppingTime.measurableSet_eq_le (hÏ„ : IsStoppingTime f Ï„) {i j : Î¹} (hle : i â‰¤ j) :
    MeasurableSet[f j] {Ï‰ | Ï„ Ï‰ = i} :=
  f.mono hle _ <| hÏ„.measurableSet_eq i

theorem IsStoppingTime.measurableSet_lt_le (hÏ„ : IsStoppingTime f Ï„) {i j : Î¹} (hle : i â‰¤ j) :
    MeasurableSet[f j] {Ï‰ | Ï„ Ï‰ < i} :=
  f.mono hle _ <| hÏ„.measurableSet_lt i

end TopologicalSpace

end LinearOrder

section Countable

theorem isStoppingTime_of_measurableSet_eq [Preorder Î¹] [Countable Î¹] {f : Filtration Î¹ m}
    {Ï„ : Î© â†’ Î¹} (hÏ„ : âˆ€ i, MeasurableSet[f i] {Ï‰ | Ï„ Ï‰ = i}) : IsStoppingTime f Ï„ := by
  intro i
  rw [show {Ï‰ | Ï„ Ï‰ â‰¤ i} = â‹ƒ k â‰¤ i, {Ï‰ | Ï„ Ï‰ = k} by ext; simp]
  refine MeasurableSet.biUnion (Set.to_countable _) fun k hk => ?_
  exact f.mono hk _ (hÏ„ k)

end Countable

end MeasurableSet

namespace IsStoppingTime

protected theorem max [LinearOrder Î¹] {f : Filtration Î¹ m} {Ï„ Ï€ : Î© â†’ Î¹} (hÏ„ : IsStoppingTime f Ï„)
    (hÏ€ : IsStoppingTime f Ï€) : IsStoppingTime f fun Ï‰ => max (Ï„ Ï‰) (Ï€ Ï‰) := by
  intro i
  simp_rw [max_le_iff, Set.setOf_and]
  exact (hÏ„ i).inter (hÏ€ i)

protected theorem max_const [LinearOrder Î¹] {f : Filtration Î¹ m} {Ï„ : Î© â†’ Î¹}
    (hÏ„ : IsStoppingTime f Ï„) (i : Î¹) : IsStoppingTime f fun Ï‰ => max (Ï„ Ï‰) i :=
  hÏ„.max (isStoppingTime_const f i)

protected theorem min [LinearOrder Î¹] {f : Filtration Î¹ m} {Ï„ Ï€ : Î© â†’ Î¹} (hÏ„ : IsStoppingTime f Ï„)
    (hÏ€ : IsStoppingTime f Ï€) : IsStoppingTime f fun Ï‰ => min (Ï„ Ï‰) (Ï€ Ï‰) := by
  intro i
  simp_rw [min_le_iff, Set.setOf_or]
  exact (hÏ„ i).union (hÏ€ i)

protected theorem min_const [LinearOrder Î¹] {f : Filtration Î¹ m} {Ï„ : Î© â†’ Î¹}
    (hÏ„ : IsStoppingTime f Ï„) (i : Î¹) : IsStoppingTime f fun Ï‰ => min (Ï„ Ï‰) i :=
  hÏ„.min (isStoppingTime_const f i)

theorem add_const [AddGroup Î¹] [Preorder Î¹] [AddRightMono Î¹]
    [AddLeftMono Î¹] {f : Filtration Î¹ m} {Ï„ : Î© â†’ Î¹} (hÏ„ : IsStoppingTime f Ï„)
    {i : Î¹} (hi : 0 â‰¤ i) : IsStoppingTime f fun Ï‰ => Ï„ Ï‰ + i := by
  intro j
  simp_rw [â† le_sub_iff_add_le]
  exact f.mono (sub_le_self j hi) _ (hÏ„ (j - i))

theorem add_const_nat {f : Filtration â„• m} {Ï„ : Î© â†’ â„•} (hÏ„ : IsStoppingTime f Ï„) {i : â„•} :
    IsStoppingTime f fun Ï‰ => Ï„ Ï‰ + i := by
  refine isStoppingTime_of_measurableSet_eq fun j => ?_
  by_cases hij : i â‰¤ j
  Â· simp_rw [eq_comm, â† Nat.sub_eq_iff_eq_add hij, eq_comm]
    exact f.mono (j.sub_le i) _ (hÏ„.measurableSet_eq (j - i))
  Â· rw [not_le] at hij
    convert @MeasurableSet.empty _ (f.1 j)
    ext Ï‰
    simp only [Set.mem_empty_iff_false, iff_false, Set.mem_setOf]
    omega

-- generalize to certain countable type?
theorem add {f : Filtration â„• m} {Ï„ Ï€ : Î© â†’ â„•} (hÏ„ : IsStoppingTime f Ï„) (hÏ€ : IsStoppingTime f Ï€) :
    IsStoppingTime f (Ï„ + Ï€) := by
  intro i
  rw [(_ : {Ï‰ | (Ï„ + Ï€) Ï‰ â‰¤ i} = â‹ƒ k â‰¤ i, {Ï‰ | Ï€ Ï‰ = k} âˆ© {Ï‰ | Ï„ Ï‰ + k â‰¤ i})]
  Â· exact MeasurableSet.iUnion fun k =>
      MeasurableSet.iUnion fun hk => (hÏ€.measurableSet_eq_le hk).inter (hÏ„.add_const_nat i)
  ext Ï‰
  simp only [Pi.add_apply, Set.mem_setOf_eq, Set.mem_iUnion, Set.mem_inter_iff, exists_prop]
  refine âŸ¨fun h => âŸ¨Ï€ Ï‰, by omega, rfl, hâŸ©, ?_âŸ©
  rintro âŸ¨j, hj, rfl, hâŸ©
  assumption

section Preorder

variable [Preorder Î¹] {f : Filtration Î¹ m} {Ï„ Ï€ : Î© â†’ Î¹}

/-- The associated Ïƒ-algebra with a stopping time. -/
protected def measurableSpace (hÏ„ : IsStoppingTime f Ï„) : MeasurableSpace Î© where
  MeasurableSet' s := âˆ€ i : Î¹, MeasurableSet[f i] (s âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ i})
  measurableSet_empty i := (Set.empty_inter {Ï‰ | Ï„ Ï‰ â‰¤ i}).symm â–¸ @MeasurableSet.empty _ (f i)
  measurableSet_compl s hs i := by
    rw [(_ : sá¶œ âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ i} = (sá¶œ âˆª {Ï‰ | Ï„ Ï‰ â‰¤ i}á¶œ) âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ i})]
    Â· refine MeasurableSet.inter ?_ ?_
      Â· rw [â† Set.compl_inter]
        exact (hs i).compl
      Â· exact hÏ„ i
    Â· rw [Set.union_inter_distrib_right]
      simp only [Set.compl_inter_self, Set.union_empty]
  measurableSet_iUnion s hs i := by
    rw [forall_swap] at hs
    rw [Set.iUnion_inter]
    exact MeasurableSet.iUnion (hs i)

protected theorem measurableSet (hÏ„ : IsStoppingTime f Ï„) (s : Set Î©) :
    MeasurableSet[hÏ„.measurableSpace] s â†” âˆ€ i : Î¹, MeasurableSet[f i] (s âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ i}) :=
  Iff.rfl

theorem measurableSpace_mono (hÏ„ : IsStoppingTime f Ï„) (hÏ€ : IsStoppingTime f Ï€) (hle : Ï„ â‰¤ Ï€) :
    hÏ„.measurableSpace â‰¤ hÏ€.measurableSpace := by
  intro s hs i
  rw [(_ : s âˆ© {Ï‰ | Ï€ Ï‰ â‰¤ i} = s âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ i} âˆ© {Ï‰ | Ï€ Ï‰ â‰¤ i})]
  Â· exact (hs i).inter (hÏ€ i)
  Â· ext
    simp only [Set.mem_inter_iff, iff_self_and, and_congr_left_iff, Set.mem_setOf_eq]
    intro hle' _
    exact le_trans (hle _) hle'

theorem measurableSpace_le_of_countable [Countable Î¹] (hÏ„ : IsStoppingTime f Ï„) :
    hÏ„.measurableSpace â‰¤ m := by
  intro s hs
  change âˆ€ i, MeasurableSet[f i] (s âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ i}) at hs
  rw [(_ : s = â‹ƒ i, s âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ i})]
  Â· exact MeasurableSet.iUnion fun i => f.le i _ (hs i)
  Â· ext Ï‰; constructor <;> rw [Set.mem_iUnion]
    Â· exact fun hx => âŸ¨Ï„ Ï‰, hx, le_rflâŸ©
    Â· rintro âŸ¨_, hx, _âŸ©
      exact hx

theorem measurableSpace_le [IsCountablyGenerated (atTop : Filter Î¹)] [IsDirected Î¹ (Â· â‰¤ Â·)]
    (hÏ„ : IsStoppingTime f Ï„) : hÏ„.measurableSpace â‰¤ m := by
  intro s hs
  cases isEmpty_or_nonempty Î¹
  Â· haveI : IsEmpty Î© := âŸ¨fun Ï‰ => IsEmpty.false (Ï„ Ï‰)âŸ©
    apply Subsingleton.measurableSet
  Â· change âˆ€ i, MeasurableSet[f i] (s âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ i}) at hs
    obtain âŸ¨seq : â„• â†’ Î¹, h_seq_tendstoâŸ© := (atTop : Filter Î¹).exists_seq_tendsto
    rw [(_ : s = â‹ƒ n, s âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ seq n})]
    Â· exact MeasurableSet.iUnion fun i => f.le (seq i) _ (hs (seq i))
    Â· ext Ï‰; constructor <;> rw [Set.mem_iUnion]
      Â· intro hx
        suffices âˆƒ i, Ï„ Ï‰ â‰¤ seq i from âŸ¨this.choose, hx, this.choose_specâŸ©
        rw [tendsto_atTop] at h_seq_tendsto
        exact (h_seq_tendsto (Ï„ Ï‰)).exists
      Â· rintro âŸ¨_, hx, _âŸ©
        exact hx

example {f : Filtration â„• m} {Ï„ : Î© â†’ â„•} (hÏ„ : IsStoppingTime f Ï„) : hÏ„.measurableSpace â‰¤ m :=
  hÏ„.measurableSpace_le

example {f : Filtration â„ m} {Ï„ : Î© â†’ â„} (hÏ„ : IsStoppingTime f Ï„) : hÏ„.measurableSpace â‰¤ m :=
  hÏ„.measurableSpace_le

@[simp]
theorem measurableSpace_const (f : Filtration Î¹ m) (i : Î¹) :
    (isStoppingTime_const f i).measurableSpace = f i := by
  ext1 s
  change MeasurableSet[(isStoppingTime_const f i).measurableSpace] s â†” MeasurableSet[f i] s
  rw [IsStoppingTime.measurableSet]
  constructor <;> intro h
  Â· specialize h i
    simpa only [le_refl, Set.setOf_true, Set.inter_univ] using h
  Â· intro j
    by_cases hij : i â‰¤ j
    Â· simp only [hij, Set.setOf_true, Set.inter_univ]
      exact f.mono hij _ h
    Â· simp only [hij, Set.setOf_false, Set.inter_empty, @MeasurableSet.empty _ (f.1 j)]

theorem measurableSet_inter_eq_iff (hÏ„ : IsStoppingTime f Ï„) (s : Set Î©) (i : Î¹) :
    MeasurableSet[hÏ„.measurableSpace] (s âˆ© {Ï‰ | Ï„ Ï‰ = i}) â†”
      MeasurableSet[f i] (s âˆ© {Ï‰ | Ï„ Ï‰ = i}) := by
  have : âˆ€ j, {Ï‰ : Î© | Ï„ Ï‰ = i} âˆ© {Ï‰ : Î© | Ï„ Ï‰ â‰¤ j} = {Ï‰ : Î© | Ï„ Ï‰ = i} âˆ© {_Ï‰ | i â‰¤ j} := by
    intro j
    ext1 Ï‰
    simp only [Set.mem_inter_iff, Set.mem_setOf_eq, and_congr_right_iff]
    intro hxi
    rw [hxi]
  constructor <;> intro h
  Â· specialize h i
    simpa only [Set.inter_assoc, this, le_refl, Set.setOf_true, Set.inter_univ] using h
  Â· intro j
    rw [Set.inter_assoc, this]
    by_cases hij : i â‰¤ j
    Â· simp only [hij, Set.setOf_true, Set.inter_univ]
      exact f.mono hij _ h
    Â· simp [hij]

theorem measurableSpace_le_of_le_const (hÏ„ : IsStoppingTime f Ï„) {i : Î¹} (hÏ„_le : âˆ€ Ï‰, Ï„ Ï‰ â‰¤ i) :
    hÏ„.measurableSpace â‰¤ f i :=
  (measurableSpace_mono hÏ„ _ hÏ„_le).trans (measurableSpace_const _ _).le

theorem measurableSpace_le_of_le (hÏ„ : IsStoppingTime f Ï„) {n : Î¹} (hÏ„_le : âˆ€ Ï‰, Ï„ Ï‰ â‰¤ n) :
    hÏ„.measurableSpace â‰¤ m :=
  (hÏ„.measurableSpace_le_of_le_const hÏ„_le).trans (f.le n)

theorem le_measurableSpace_of_const_le (hÏ„ : IsStoppingTime f Ï„) {i : Î¹} (hÏ„_le : âˆ€ Ï‰, i â‰¤ Ï„ Ï‰) :
    f i â‰¤ hÏ„.measurableSpace :=
  (measurableSpace_const _ _).symm.le.trans (measurableSpace_mono _ hÏ„ hÏ„_le)

end Preorder

instance sigmaFinite_stopping_time {Î¹} [SemilatticeSup Î¹] [OrderBot Î¹]
    [(Filter.atTop : Filter Î¹).IsCountablyGenerated] {Î¼ : Measure Î©} {f : Filtration Î¹ m}
    {Ï„ : Î© â†’ Î¹} [SigmaFiniteFiltration Î¼ f] (hÏ„ : IsStoppingTime f Ï„) :
    SigmaFinite (Î¼.trim hÏ„.measurableSpace_le) := by
  refine @sigmaFiniteTrim_mono _ _ ?_ _ _ _ ?_ ?_
  Â· exact f âŠ¥
  Â· exact hÏ„.le_measurableSpace_of_const_le fun _ => bot_le
  Â· infer_instance

instance sigmaFinite_stopping_time_of_le {Î¹} [SemilatticeSup Î¹] [OrderBot Î¹] {Î¼ : Measure Î©}
    {f : Filtration Î¹ m} {Ï„ : Î© â†’ Î¹} [SigmaFiniteFiltration Î¼ f] (hÏ„ : IsStoppingTime f Ï„) {n : Î¹}
    (hÏ„_le : âˆ€ Ï‰, Ï„ Ï‰ â‰¤ n) : SigmaFinite (Î¼.trim (hÏ„.measurableSpace_le_of_le hÏ„_le)) := by
  refine @sigmaFiniteTrim_mono _ _ ?_ _ _ _ ?_ ?_
  Â· exact f âŠ¥
  Â· exact hÏ„.le_measurableSpace_of_const_le fun _ => bot_le
  Â· infer_instance

section LinearOrder

variable [LinearOrder Î¹] {f : Filtration Î¹ m} {Ï„ Ï€ : Î© â†’ Î¹}

protected theorem measurableSet_le' (hÏ„ : IsStoppingTime f Ï„) (i : Î¹) :
    MeasurableSet[hÏ„.measurableSpace] {Ï‰ | Ï„ Ï‰ â‰¤ i} := by
  intro j
  have : {Ï‰ : Î© | Ï„ Ï‰ â‰¤ i} âˆ© {Ï‰ : Î© | Ï„ Ï‰ â‰¤ j} = {Ï‰ : Î© | Ï„ Ï‰ â‰¤ min i j} := by
    ext1 Ï‰; simp only [Set.mem_inter_iff, Set.mem_setOf_eq, le_min_iff]
  rw [this]
  exact f.mono (min_le_right i j) _ (hÏ„ _)

protected theorem measurableSet_gt' (hÏ„ : IsStoppingTime f Ï„) (i : Î¹) :
    MeasurableSet[hÏ„.measurableSpace] {Ï‰ | i < Ï„ Ï‰} := by
  have : {Ï‰ : Î© | i < Ï„ Ï‰} = {Ï‰ : Î© | Ï„ Ï‰ â‰¤ i}á¶œ := by ext1 Ï‰; simp
  rw [this]
  exact (hÏ„.measurableSet_le' i).compl

protected theorem measurableSet_eq' [TopologicalSpace Î¹] [OrderTopology Î¹]
    [FirstCountableTopology Î¹] (hÏ„ : IsStoppingTime f Ï„) (i : Î¹) :
    MeasurableSet[hÏ„.measurableSpace] {Ï‰ | Ï„ Ï‰ = i} := by
  rw [â† Set.univ_inter {Ï‰ | Ï„ Ï‰ = i}, measurableSet_inter_eq_iff, Set.univ_inter]
  exact hÏ„.measurableSet_eq i

protected theorem measurableSet_ge' [TopologicalSpace Î¹] [OrderTopology Î¹]
    [FirstCountableTopology Î¹] (hÏ„ : IsStoppingTime f Ï„) (i : Î¹) :
    MeasurableSet[hÏ„.measurableSpace] {Ï‰ | i â‰¤ Ï„ Ï‰} := by
  have : {Ï‰ | i â‰¤ Ï„ Ï‰} = {Ï‰ | Ï„ Ï‰ = i} âˆª {Ï‰ | i < Ï„ Ï‰} := by
    ext1 Ï‰
    simp only [le_iff_lt_or_eq, Set.mem_setOf_eq, Set.mem_union]
    rw [@eq_comm _ i, or_comm]
  rw [this]
  exact (hÏ„.measurableSet_eq' i).union (hÏ„.measurableSet_gt' i)

protected theorem measurableSet_lt' [TopologicalSpace Î¹] [OrderTopology Î¹]
    [FirstCountableTopology Î¹] (hÏ„ : IsStoppingTime f Ï„) (i : Î¹) :
    MeasurableSet[hÏ„.measurableSpace] {Ï‰ | Ï„ Ï‰ < i} := by
  have : {Ï‰ | Ï„ Ï‰ < i} = {Ï‰ | Ï„ Ï‰ â‰¤ i} \ {Ï‰ | Ï„ Ï‰ = i} := by
    ext1 Ï‰
    simp only [lt_iff_le_and_ne, Set.mem_setOf_eq, Set.mem_diff]
  rw [this]
  exact (hÏ„.measurableSet_le' i).diff (hÏ„.measurableSet_eq' i)

section Countable

protected theorem measurableSet_eq_of_countable_range' (hÏ„ : IsStoppingTime f Ï„)
    (h_countable : (Set.range Ï„).Countable) (i : Î¹) :
    MeasurableSet[hÏ„.measurableSpace] {Ï‰ | Ï„ Ï‰ = i} := by
  rw [â† Set.univ_inter {Ï‰ | Ï„ Ï‰ = i}, measurableSet_inter_eq_iff, Set.univ_inter]
  exact hÏ„.measurableSet_eq_of_countable_range h_countable i

protected theorem measurableSet_eq_of_countable' [Countable Î¹] (hÏ„ : IsStoppingTime f Ï„) (i : Î¹) :
    MeasurableSet[hÏ„.measurableSpace] {Ï‰ | Ï„ Ï‰ = i} :=
  hÏ„.measurableSet_eq_of_countable_range' (Set.to_countable _) i

protected theorem measurableSet_ge_of_countable_range' (hÏ„ : IsStoppingTime f Ï„)
    (h_countable : (Set.range Ï„).Countable) (i : Î¹) :
    MeasurableSet[hÏ„.measurableSpace] {Ï‰ | i â‰¤ Ï„ Ï‰} := by
  have : {Ï‰ | i â‰¤ Ï„ Ï‰} = {Ï‰ | Ï„ Ï‰ = i} âˆª {Ï‰ | i < Ï„ Ï‰} := by
    ext1 Ï‰
    simp only [le_iff_lt_or_eq, Set.mem_setOf_eq, Set.mem_union]
    rw [@eq_comm _ i, or_comm]
  rw [this]
  exact (hÏ„.measurableSet_eq_of_countable_range' h_countable i).union (hÏ„.measurableSet_gt' i)

protected theorem measurableSet_ge_of_countable' [Countable Î¹] (hÏ„ : IsStoppingTime f Ï„) (i : Î¹) :
    MeasurableSet[hÏ„.measurableSpace] {Ï‰ | i â‰¤ Ï„ Ï‰} :=
  hÏ„.measurableSet_ge_of_countable_range' (Set.to_countable _) i

protected theorem measurableSet_lt_of_countable_range' (hÏ„ : IsStoppingTime f Ï„)
    (h_countable : (Set.range Ï„).Countable) (i : Î¹) :
    MeasurableSet[hÏ„.measurableSpace] {Ï‰ | Ï„ Ï‰ < i} := by
  have : {Ï‰ | Ï„ Ï‰ < i} = {Ï‰ | Ï„ Ï‰ â‰¤ i} \ {Ï‰ | Ï„ Ï‰ = i} := by
    ext1 Ï‰
    simp only [lt_iff_le_and_ne, Set.mem_setOf_eq, Set.mem_diff]
  rw [this]
  exact (hÏ„.measurableSet_le' i).diff (hÏ„.measurableSet_eq_of_countable_range' h_countable i)

protected theorem measurableSet_lt_of_countable' [Countable Î¹] (hÏ„ : IsStoppingTime f Ï„) (i : Î¹) :
    MeasurableSet[hÏ„.measurableSpace] {Ï‰ | Ï„ Ï‰ < i} :=
  hÏ„.measurableSet_lt_of_countable_range' (Set.to_countable _) i

protected theorem measurableSpace_le_of_countable_range (hÏ„ : IsStoppingTime f Ï„)
    (h_countable : (Set.range Ï„).Countable) : hÏ„.measurableSpace â‰¤ m := by
  intro s hs
  change âˆ€ i, MeasurableSet[f i] (s âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ i}) at hs
  rw [(_ : s = â‹ƒ i âˆˆ Set.range Ï„, s âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ i})]
  Â· exact MeasurableSet.biUnion h_countable fun i _ => f.le i _ (hs i)
  Â· ext Ï‰
    constructor <;> rw [Set.mem_iUnion]
    Â· exact fun hx => âŸ¨Ï„ Ï‰, by simpa using hxâŸ©
    Â· rintro âŸ¨i, hxâŸ©
      simp only [Set.mem_range, Set.iUnion_exists, Set.mem_iUnion, Set.mem_inter_iff,
        Set.mem_setOf_eq, exists_prop, exists_and_right] at hx
      exact hx.2.1

end Countable

protected theorem measurable [TopologicalSpace Î¹] [MeasurableSpace Î¹] [BorelSpace Î¹]
    [OrderTopology Î¹] [SecondCountableTopology Î¹] (hÏ„ : IsStoppingTime f Ï„) :
    Measurable[hÏ„.measurableSpace] Ï„ :=
  @measurable_of_Iic Î¹ Î© _ _ _ hÏ„.measurableSpace _ _ _ _ fun i => hÏ„.measurableSet_le' i

protected theorem measurable_of_le [TopologicalSpace Î¹] [MeasurableSpace Î¹] [BorelSpace Î¹]
    [OrderTopology Î¹] [SecondCountableTopology Î¹] (hÏ„ : IsStoppingTime f Ï„) {i : Î¹}
    (hÏ„_le : âˆ€ Ï‰, Ï„ Ï‰ â‰¤ i) : Measurable[f i] Ï„ :=
  hÏ„.measurable.mono (measurableSpace_le_of_le_const _ hÏ„_le) le_rfl

theorem measurableSpace_min (hÏ„ : IsStoppingTime f Ï„) (hÏ€ : IsStoppingTime f Ï€) :
    (hÏ„.min hÏ€).measurableSpace = hÏ„.measurableSpace âŠ“ hÏ€.measurableSpace := by
  refine le_antisymm ?_ ?_
  Â· exact le_inf (measurableSpace_mono _ hÏ„ fun _ => min_le_left _ _)
      (measurableSpace_mono _ hÏ€ fun _ => min_le_right _ _)
  Â· intro s
    change MeasurableSet[hÏ„.measurableSpace] s âˆ§ MeasurableSet[hÏ€.measurableSpace] s â†’
      MeasurableSet[(hÏ„.min hÏ€).measurableSpace] s
    simp_rw [IsStoppingTime.measurableSet]
    have : âˆ€ i, {Ï‰ | min (Ï„ Ï‰) (Ï€ Ï‰) â‰¤ i} = {Ï‰ | Ï„ Ï‰ â‰¤ i} âˆª {Ï‰ | Ï€ Ï‰ â‰¤ i} := by
      intro i; ext1 Ï‰; simp
    simp_rw [this, Set.inter_union_distrib_left]
    exact fun h i => (h.left i).union (h.right i)

theorem measurableSet_min_iff (hÏ„ : IsStoppingTime f Ï„) (hÏ€ : IsStoppingTime f Ï€) (s : Set Î©) :
    MeasurableSet[(hÏ„.min hÏ€).measurableSpace] s â†”
      MeasurableSet[hÏ„.measurableSpace] s âˆ§ MeasurableSet[hÏ€.measurableSpace] s := by
  rw [measurableSpace_min hÏ„ hÏ€]; rfl

theorem measurableSpace_min_const (hÏ„ : IsStoppingTime f Ï„) {i : Î¹} :
    (hÏ„.min_const i).measurableSpace = hÏ„.measurableSpace âŠ“ f i := by
  rw [hÏ„.measurableSpace_min (isStoppingTime_const _ i), measurableSpace_const]

theorem measurableSet_min_const_iff (hÏ„ : IsStoppingTime f Ï„) (s : Set Î©) {i : Î¹} :
    MeasurableSet[(hÏ„.min_const i).measurableSpace] s â†”
      MeasurableSet[hÏ„.measurableSpace] s âˆ§ MeasurableSet[f i] s := by
  rw [measurableSpace_min_const hÏ„]; apply MeasurableSpace.measurableSet_inf

theorem measurableSet_inter_le [TopologicalSpace Î¹] [SecondCountableTopology Î¹] [OrderTopology Î¹]
    [MeasurableSpace Î¹] [BorelSpace Î¹] (hÏ„ : IsStoppingTime f Ï„) (hÏ€ : IsStoppingTime f Ï€)
    (s : Set Î©) (hs : MeasurableSet[hÏ„.measurableSpace] s) :
    MeasurableSet[(hÏ„.min hÏ€).measurableSpace] (s âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ Ï€ Ï‰}) := by
  simp_rw [IsStoppingTime.measurableSet] at hs âŠ¢
  intro i
  have : s âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ Ï€ Ï‰} âˆ© {Ï‰ | min (Ï„ Ï‰) (Ï€ Ï‰) â‰¤ i} =
      s âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ i} âˆ© {Ï‰ | min (Ï„ Ï‰) (Ï€ Ï‰) â‰¤ i} âˆ©
        {Ï‰ | min (Ï„ Ï‰) i â‰¤ min (min (Ï„ Ï‰) (Ï€ Ï‰)) i} := by
    ext1 Ï‰
    simp only [min_le_iff, Set.mem_inter_iff, Set.mem_setOf_eq, le_min_iff, le_refl, true_and,
      true_or]
    by_cases hÏ„i : Ï„ Ï‰ â‰¤ i
    Â· simp only [hÏ„i, true_or, and_true, and_congr_right_iff]
      intro
      constructor <;> intro h
      Â· exact Or.inl h
      Â· rcases h with h | h
        Â· exact h
        Â· exact hÏ„i.trans h
    simp only [hÏ„i, false_or, and_false, false_and, iff_false, not_and, not_le, and_imp]
    refine fun _ hÏ„_le_Ï€ => lt_of_lt_of_le ?_ hÏ„_le_Ï€
    rw [â† not_le]
    exact hÏ„i
  rw [this]
  refine ((hs i).inter ((hÏ„.min hÏ€) i)).inter ?_
  apply @measurableSet_le _ _ _ _ _ (Filtration.seq f i) _ _ _ _ _ ?_ ?_
  Â· exact (hÏ„.min_const i).measurable_of_le fun _ => min_le_right _ _
  Â· exact ((hÏ„.min hÏ€).min_const i).measurable_of_le fun _ => min_le_right _ _

theorem measurableSet_inter_le_iff [TopologicalSpace Î¹] [SecondCountableTopology Î¹]
    [OrderTopology Î¹] [MeasurableSpace Î¹] [BorelSpace Î¹] (hÏ„ : IsStoppingTime f Ï„)
    (hÏ€ : IsStoppingTime f Ï€) (s : Set Î©) :
    MeasurableSet[hÏ„.measurableSpace] (s âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ Ï€ Ï‰}) â†”
      MeasurableSet[(hÏ„.min hÏ€).measurableSpace] (s âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ Ï€ Ï‰}) := by
  constructor <;> intro h
  Â· have : s âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ Ï€ Ï‰} = s âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ Ï€ Ï‰} âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ Ï€ Ï‰} := by
      rw [Set.inter_assoc, Set.inter_self]
    rw [this]
    exact measurableSet_inter_le _ hÏ€ _ h
  Â· rw [measurableSet_min_iff hÏ„ hÏ€] at h
    exact h.1

theorem measurableSet_inter_le_const_iff (hÏ„ : IsStoppingTime f Ï„) (s : Set Î©) (i : Î¹) :
    MeasurableSet[hÏ„.measurableSpace] (s âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ i}) â†”
      MeasurableSet[(hÏ„.min_const i).measurableSpace] (s âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ i}) := by
  rw [IsStoppingTime.measurableSet_min_iff hÏ„ (isStoppingTime_const _ i),
    IsStoppingTime.measurableSpace_const, IsStoppingTime.measurableSet]
  refine âŸ¨fun h => âŸ¨h, ?_âŸ©, fun h j => h.1 jâŸ©
  specialize h i
  rwa [Set.inter_assoc, Set.inter_self] at h

theorem measurableSet_le_stopping_time [TopologicalSpace Î¹] [SecondCountableTopology Î¹]
    [OrderTopology Î¹] [MeasurableSpace Î¹] [BorelSpace Î¹] (hÏ„ : IsStoppingTime f Ï„)
    (hÏ€ : IsStoppingTime f Ï€) : MeasurableSet[hÏ„.measurableSpace] {Ï‰ | Ï„ Ï‰ â‰¤ Ï€ Ï‰} := by
  rw [hÏ„.measurableSet]
  intro j
  have : {Ï‰ | Ï„ Ï‰ â‰¤ Ï€ Ï‰} âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ j} = {Ï‰ | min (Ï„ Ï‰) j â‰¤ min (Ï€ Ï‰) j} âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ j} := by
    ext1 Ï‰
    simp only [Set.mem_inter_iff, Set.mem_setOf_eq, min_le_iff, le_min_iff, le_refl,
      and_congr_left_iff]
    intro h
    simp only [h, or_self_iff, and_true]
    rw [Iff.comm, or_iff_left_iff_imp]
    exact h.trans
  rw [this]
  refine MeasurableSet.inter ?_ (hÏ„.measurableSet_le j)
  apply @measurableSet_le _ _ _ _ _ (Filtration.seq f j) _ _ _ _ _ ?_ ?_
  Â· exact (hÏ„.min_const j).measurable_of_le fun _ => min_le_right _ _
  Â· exact (hÏ€.min_const j).measurable_of_le fun _ => min_le_right _ _

theorem measurableSet_stopping_time_le [TopologicalSpace Î¹] [SecondCountableTopology Î¹]
    [OrderTopology Î¹] [MeasurableSpace Î¹] [BorelSpace Î¹] (hÏ„ : IsStoppingTime f Ï„)
    (hÏ€ : IsStoppingTime f Ï€) : MeasurableSet[hÏ€.measurableSpace] {Ï‰ | Ï„ Ï‰ â‰¤ Ï€ Ï‰} := by
  suffices MeasurableSet[(hÏ„.min hÏ€).measurableSpace] {Ï‰ : Î© | Ï„ Ï‰ â‰¤ Ï€ Ï‰} by
    rw [measurableSet_min_iff hÏ„ hÏ€] at this; exact this.2
  rw [â† Set.univ_inter {Ï‰ : Î© | Ï„ Ï‰ â‰¤ Ï€ Ï‰}, â† hÏ„.measurableSet_inter_le_iff hÏ€, Set.univ_inter]
  exact measurableSet_le_stopping_time hÏ„ hÏ€

theorem measurableSet_eq_stopping_time [AddGroup Î¹] [TopologicalSpace Î¹] [MeasurableSpace Î¹]
    [BorelSpace Î¹] [OrderTopology Î¹] [MeasurableSingletonClass Î¹] [SecondCountableTopology Î¹]
    [MeasurableSubâ‚‚ Î¹] (hÏ„ : IsStoppingTime f Ï„) (hÏ€ : IsStoppingTime f Ï€) :
    MeasurableSet[hÏ„.measurableSpace] {Ï‰ | Ï„ Ï‰ = Ï€ Ï‰} := by
  rw [hÏ„.measurableSet]
  intro j
  have : {Ï‰ | Ï„ Ï‰ = Ï€ Ï‰} âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ j} =
      {Ï‰ | min (Ï„ Ï‰) j = min (Ï€ Ï‰) j} âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ j} âˆ© {Ï‰ | Ï€ Ï‰ â‰¤ j} := by
    ext1 Ï‰
    simp only [Set.mem_inter_iff, Set.mem_setOf_eq]
    refine âŸ¨fun h => âŸ¨âŸ¨?_, h.2âŸ©, ?_âŸ©, fun h => âŸ¨?_, h.1.2âŸ©âŸ©
    Â· rw [h.1]
    Â· rw [â† h.1]; exact h.2
    Â· obtain âŸ¨h', hÏƒ_leâŸ© := h
      obtain âŸ¨h_eq, hÏ„_leâŸ© := h'
      rwa [min_eq_left hÏ„_le, min_eq_left hÏƒ_le] at h_eq
  rw [this]
  refine
    MeasurableSet.inter (MeasurableSet.inter ?_ (hÏ„.measurableSet_le j)) (hÏ€.measurableSet_le j)
  apply measurableSet_eq_fun
  Â· exact (hÏ„.min_const j).measurable_of_le fun _ => min_le_right _ _
  Â· exact (hÏ€.min_const j).measurable_of_le fun _ => min_le_right _ _

theorem measurableSet_eq_stopping_time_of_countable [Countable Î¹] [TopologicalSpace Î¹]
    [MeasurableSpace Î¹] [BorelSpace Î¹] [OrderTopology Î¹] [MeasurableSingletonClass Î¹]
    [SecondCountableTopology Î¹] (hÏ„ : IsStoppingTime f Ï„) (hÏ€ : IsStoppingTime f Ï€) :
    MeasurableSet[hÏ„.measurableSpace] {Ï‰ | Ï„ Ï‰ = Ï€ Ï‰} := by
  rw [hÏ„.measurableSet]
  intro j
  have : {Ï‰ | Ï„ Ï‰ = Ï€ Ï‰} âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ j} =
      {Ï‰ | min (Ï„ Ï‰) j = min (Ï€ Ï‰) j} âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ j} âˆ© {Ï‰ | Ï€ Ï‰ â‰¤ j} := by
    ext1 Ï‰
    simp only [Set.mem_inter_iff, Set.mem_setOf_eq]
    refine âŸ¨fun h => âŸ¨âŸ¨?_, h.2âŸ©, ?_âŸ©, fun h => âŸ¨?_, h.1.2âŸ©âŸ©
    Â· rw [h.1]
    Â· rw [â† h.1]; exact h.2
    Â· obtain âŸ¨h', hÏ€_leâŸ© := h
      obtain âŸ¨h_eq, hÏ„_leâŸ© := h'
      rwa [min_eq_left hÏ„_le, min_eq_left hÏ€_le] at h_eq
  rw [this]
  refine
    MeasurableSet.inter (MeasurableSet.inter ?_ (hÏ„.measurableSet_le j)) (hÏ€.measurableSet_le j)
  apply measurableSet_eq_fun_of_countable
  Â· exact (hÏ„.min_const j).measurable_of_le fun _ => min_le_right _ _
  Â· exact (hÏ€.min_const j).measurable_of_le fun _ => min_le_right _ _

end LinearOrder

end IsStoppingTime

section LinearOrder

/-! ## Stopped value and stopped process -/


/-- Given a map `u : Î¹ â†’ Î© â†’ E`, its stopped value with respect to the stopping
time `Ï„` is the map `x â†¦ u (Ï„ Ï‰) Ï‰`. -/
def stoppedValue (u : Î¹ â†’ Î© â†’ Î²) (Ï„ : Î© â†’ Î¹) : Î© â†’ Î² := fun Ï‰ => u (Ï„ Ï‰) Ï‰

theorem stoppedValue_const (u : Î¹ â†’ Î© â†’ Î²) (i : Î¹) : (stoppedValue u fun _ => i) = u i :=
  rfl

variable [LinearOrder Î¹]

/-- Given a map `u : Î¹ â†’ Î© â†’ E`, the stopped process with respect to `Ï„` is `u i Ï‰` if
`i â‰¤ Ï„ Ï‰`, and `u (Ï„ Ï‰) Ï‰` otherwise.

Intuitively, the stopped process stops evolving once the stopping time has occurred. -/
def stoppedProcess (u : Î¹ â†’ Î© â†’ Î²) (Ï„ : Î© â†’ Î¹) : Î¹ â†’ Î© â†’ Î² := fun i Ï‰ => u (min i (Ï„ Ï‰)) Ï‰

theorem stoppedProcess_eq_stoppedValue {u : Î¹ â†’ Î© â†’ Î²} {Ï„ : Î© â†’ Î¹} :
    stoppedProcess u Ï„ = fun i => stoppedValue u fun Ï‰ => min i (Ï„ Ï‰) :=
  rfl

theorem stoppedValue_stoppedProcess {u : Î¹ â†’ Î© â†’ Î²} {Ï„ Ïƒ : Î© â†’ Î¹} :
    stoppedValue (stoppedProcess u Ï„) Ïƒ = stoppedValue u fun Ï‰ => min (Ïƒ Ï‰) (Ï„ Ï‰) :=
  rfl

theorem stoppedProcess_eq_of_le {u : Î¹ â†’ Î© â†’ Î²} {Ï„ : Î© â†’ Î¹} {i : Î¹} {Ï‰ : Î©} (h : i â‰¤ Ï„ Ï‰) :
    stoppedProcess u Ï„ i Ï‰ = u i Ï‰ := by simp [stoppedProcess, min_eq_left h]

theorem stoppedProcess_eq_of_ge {u : Î¹ â†’ Î© â†’ Î²} {Ï„ : Î© â†’ Î¹} {i : Î¹} {Ï‰ : Î©} (h : Ï„ Ï‰ â‰¤ i) :
    stoppedProcess u Ï„ i Ï‰ = u (Ï„ Ï‰) Ï‰ := by simp [stoppedProcess, min_eq_right h]

section ProgMeasurable

variable [MeasurableSpace Î¹] [TopologicalSpace Î¹] [OrderTopology Î¹] [SecondCountableTopology Î¹]
  [BorelSpace Î¹] [TopologicalSpace Î²] {u : Î¹ â†’ Î© â†’ Î²} {Ï„ : Î© â†’ Î¹} {f : Filtration Î¹ m}

theorem progMeasurable_min_stopping_time [PseudoMetrizableSpace Î¹] (hÏ„ : IsStoppingTime f Ï„) :
    ProgMeasurable f fun i Ï‰ => min i (Ï„ Ï‰) := by
  intro i
  let m_prod : MeasurableSpace (Set.Iic i Ã— Î©) := Subtype.instMeasurableSpace.prod (f i)
  let m_set : âˆ€ t : Set (Set.Iic i Ã— Î©), MeasurableSpace t := fun _ =>
    @Subtype.instMeasurableSpace (Set.Iic i Ã— Î©) _ m_prod
  let s := {p : Set.Iic i Ã— Î© | Ï„ p.2 â‰¤ i}
  have hs : MeasurableSet[m_prod] s := @measurable_snd (Set.Iic i) Î© _ (f i) _ (hÏ„ i)
  have h_meas_fst : âˆ€ t : Set (Set.Iic i Ã— Î©),
      Measurable[m_set t] fun x : t => ((x : Set.Iic i Ã— Î©).fst : Î¹) :=
    fun t => (@measurable_subtype_coe (Set.Iic i Ã— Î©) m_prod _).fst.subtype_val
  apply Measurable.stronglyMeasurable
  refine measurable_of_restrict_of_restrict_compl hs ?_ ?_
  Â· refine @Measurable.min _ _ _ _ _ (m_set s) _ _ _ _ _ (h_meas_fst s) ?_
    refine @measurable_of_Iic Î¹ s _ _ _ (m_set s) _ _ _ _ fun j => ?_
    have h_set_eq : (fun x : s => Ï„ (x : Set.Iic i Ã— Î©).snd) â»Â¹' Set.Iic j =
        (fun x : s => (x : Set.Iic i Ã— Î©).snd) â»Â¹' {Ï‰ | Ï„ Ï‰ â‰¤ min i j} := by
      ext1 Ï‰
      simp only [Set.mem_preimage, Set.mem_Iic, iff_and_self, le_min_iff, Set.mem_setOf_eq]
      exact fun _ => Ï‰.prop
    rw [h_set_eq]
    suffices h_meas : @Measurable _ _ (m_set s) (f i) fun x : s â†¦ (x : Set.Iic i Ã— Î©).snd from
      h_meas (f.mono (min_le_left _ _) _ (hÏ„.measurableSet_le (min i j)))
    exact measurable_snd.comp (@measurable_subtype_coe _ m_prod _)
  Â· letI sc := sá¶œ
    suffices h_min_eq_left :
      (fun x : sc => min (â†‘(x : Set.Iic i Ã— Î©).fst) (Ï„ (x : Set.Iic i Ã— Î©).snd)) = fun x : sc =>
        â†‘(x : Set.Iic i Ã— Î©).fst by
      simp +unfoldPartialApp only [sc, Set.restrict, h_min_eq_left]
      exact h_meas_fst _
    ext1 Ï‰
    rw [min_eq_left]
    have hx_fst_le : â†‘(Ï‰ : Set.Iic i Ã— Î©).fst â‰¤ i := (Ï‰ : Set.Iic i Ã— Î©).fst.prop
    refine hx_fst_le.trans (le_of_lt ?_)
    convert Ï‰.prop
    simp only [sc, s, not_le, Set.mem_compl_iff, Set.mem_setOf_eq]

theorem ProgMeasurable.stoppedProcess [PseudoMetrizableSpace Î¹] (h : ProgMeasurable f u)
    (hÏ„ : IsStoppingTime f Ï„) : ProgMeasurable f (stoppedProcess u Ï„) :=
  h.comp (progMeasurable_min_stopping_time hÏ„) fun _ _ => min_le_left _ _

theorem ProgMeasurable.adapted_stoppedProcess [PseudoMetrizableSpace Î¹] (h : ProgMeasurable f u)
    (hÏ„ : IsStoppingTime f Ï„) : Adapted f (MeasureTheory.stoppedProcess u Ï„) :=
  (h.stoppedProcess hÏ„).adapted

theorem ProgMeasurable.stronglyMeasurable_stoppedProcess [PseudoMetrizableSpace Î¹]
    (hu : ProgMeasurable f u) (hÏ„ : IsStoppingTime f Ï„) (i : Î¹) :
    StronglyMeasurable (MeasureTheory.stoppedProcess u Ï„ i) :=
  (hu.adapted_stoppedProcess hÏ„ i).mono (f.le _)

theorem stronglyMeasurable_stoppedValue_of_le (h : ProgMeasurable f u) (hÏ„ : IsStoppingTime f Ï„)
    {n : Î¹} (hÏ„_le : âˆ€ Ï‰, Ï„ Ï‰ â‰¤ n) : StronglyMeasurable[f n] (stoppedValue u Ï„) := by
  have : stoppedValue u Ï„ =
      (fun p : Set.Iic n Ã— Î© => u (â†‘p.fst) p.snd) âˆ˜ fun Ï‰ => (âŸ¨Ï„ Ï‰, hÏ„_le Ï‰âŸ©, Ï‰) := by
    ext1 Ï‰; simp only [stoppedValue, Function.comp_apply]
  rw [this]
  refine StronglyMeasurable.comp_measurable (h n) ?_
  exact (hÏ„.measurable_of_le hÏ„_le).subtype_mk.prodMk measurable_id

theorem measurable_stoppedValue [PseudoMetrizableSpace Î²] [MeasurableSpace Î²] [BorelSpace Î²]
    (hf_prog : ProgMeasurable f u) (hÏ„ : IsStoppingTime f Ï„) :
    Measurable[hÏ„.measurableSpace] (stoppedValue u Ï„) := by
  have h_str_meas : âˆ€ i, StronglyMeasurable[f i] (stoppedValue u fun Ï‰ => min (Ï„ Ï‰) i) := fun i =>
    stronglyMeasurable_stoppedValue_of_le hf_prog (hÏ„.min_const i) fun _ => min_le_right _ _
  intro t ht i
  suffices stoppedValue u Ï„ â»Â¹' t âˆ© {Ï‰ : Î© | Ï„ Ï‰ â‰¤ i} =
      (stoppedValue u fun Ï‰ => min (Ï„ Ï‰) i) â»Â¹' t âˆ© {Ï‰ : Î© | Ï„ Ï‰ â‰¤ i} by
    rw [this]; exact ((h_str_meas i).measurable ht).inter (hÏ„.measurableSet_le i)
  ext1 Ï‰
  simp only [stoppedValue, Set.mem_inter_iff, Set.mem_preimage, Set.mem_setOf_eq,
    and_congr_left_iff]
  intro h
  rw [min_eq_left h]

end ProgMeasurable

end LinearOrder

section StoppedValueOfMemFinset

variable {Î¼ : Measure Î©} {Ï„ : Î© â†’ Î¹} {E : Type*} {p : â„â‰¥0âˆ} {u : Î¹ â†’ Î© â†’ E}

theorem stoppedValue_eq_of_mem_finset [AddCommMonoid E] {s : Finset Î¹} (hbdd : âˆ€ Ï‰, Ï„ Ï‰ âˆˆ s) :
    stoppedValue u Ï„ = âˆ‘ i âˆˆ s, Set.indicator {Ï‰ | Ï„ Ï‰ = i} (u i) := by
  ext y
  classical
  rw [stoppedValue, Finset.sum_apply, Finset.sum_indicator_eq_sum_filter]
  suffices {i âˆˆ s | y âˆˆ {Ï‰ : Î© | Ï„ Ï‰ = i}} = ({Ï„ y} : Finset Î¹) by
    rw [this, Finset.sum_singleton]
  ext1 Ï‰
  simp only [Set.mem_setOf_eq, Finset.mem_filter, Finset.mem_singleton]
  constructor <;> intro h
  Â· exact h.2.symm
  Â· refine âŸ¨?_, h.symmâŸ©; rw [h]; exact hbdd y

theorem stoppedValue_eq' [Preorder Î¹] [LocallyFiniteOrderBot Î¹] [AddCommMonoid E] {N : Î¹}
    (hbdd : âˆ€ Ï‰, Ï„ Ï‰ â‰¤ N) :
    stoppedValue u Ï„ = âˆ‘ i âˆˆ Finset.Iic N, Set.indicator {Ï‰ | Ï„ Ï‰ = i} (u i) :=
  stoppedValue_eq_of_mem_finset fun Ï‰ => Finset.mem_Iic.mpr (hbdd Ï‰)

theorem stoppedProcess_eq_of_mem_finset [LinearOrder Î¹] [AddCommMonoid E] {s : Finset Î¹} (n : Î¹)
    (hbdd : âˆ€ Ï‰, Ï„ Ï‰ < n â†’ Ï„ Ï‰ âˆˆ s) : stoppedProcess u Ï„ n = Set.indicator {a | n â‰¤ Ï„ a} (u n) +
      âˆ‘ i âˆˆ s with i < n, Set.indicator {Ï‰ | Ï„ Ï‰ = i} (u i) := by
  ext Ï‰
  rw [Pi.add_apply, Finset.sum_apply]
  rcases le_or_gt n (Ï„ Ï‰) with h | h
  Â· rw [stoppedProcess_eq_of_le h, Set.indicator_of_mem, Finset.sum_eq_zero, add_zero]
    Â· intro m hm
      refine Set.indicator_of_notMem ?_ _
      rw [Finset.mem_filter] at hm
      exact (hm.2.trans_le h).ne'
    Â· exact h
  Â· rw [stoppedProcess_eq_of_ge (le_of_lt h), Finset.sum_eq_single_of_mem (Ï„ Ï‰)]
    Â· rw [Set.indicator_of_notMem, zero_add, Set.indicator_of_mem] <;> rw [Set.mem_setOf]
      exact not_le.2 h
    Â· rw [Finset.mem_filter]
      exact âŸ¨hbdd Ï‰ h, hâŸ©
    Â· intro b _ hneq
      rw [Set.indicator_of_notMem]
      rw [Set.mem_setOf]
      exact hneq.symm

theorem stoppedProcess_eq'' [LinearOrder Î¹] [LocallyFiniteOrderBot Î¹] [AddCommMonoid E] (n : Î¹) :
    stoppedProcess u Ï„ n = Set.indicator {a | n â‰¤ Ï„ a} (u n) +
      âˆ‘ i âˆˆ Finset.Iio n, Set.indicator {Ï‰ | Ï„ Ï‰ = i} (u i) := by
  have h_mem : âˆ€ Ï‰, Ï„ Ï‰ < n â†’ Ï„ Ï‰ âˆˆ Finset.Iio n := fun Ï‰ h => Finset.mem_Iio.mpr h
  rw [stoppedProcess_eq_of_mem_finset n h_mem]
  congr with i
  simp

section StoppedValue

variable [PartialOrder Î¹] {â„± : Filtration Î¹ m} [NormedAddCommGroup E]

theorem memLp_stoppedValue_of_mem_finset (hÏ„ : IsStoppingTime â„± Ï„) (hu : âˆ€ n, MemLp (u n) p Î¼)
    {s : Finset Î¹} (hbdd : âˆ€ Ï‰, Ï„ Ï‰ âˆˆ s) : MemLp (stoppedValue u Ï„) p Î¼ := by
  rw [stoppedValue_eq_of_mem_finset hbdd]
  refine memLp_finset_sum' _ fun i _ => MemLp.indicator ?_ (hu i)
  refine â„±.le i {a : Î© | Ï„ a = i} (hÏ„.measurableSet_eq_of_countable_range ?_ i)
  refine ((Finset.finite_toSet s).subset fun Ï‰ hÏ‰ => ?_).countable
  obtain âŸ¨y, rflâŸ© := hÏ‰
  exact hbdd y

theorem memLp_stoppedValue [LocallyFiniteOrderBot Î¹] (hÏ„ : IsStoppingTime â„± Ï„)
    (hu : âˆ€ n, MemLp (u n) p Î¼) {N : Î¹} (hbdd : âˆ€ Ï‰, Ï„ Ï‰ â‰¤ N) : MemLp (stoppedValue u Ï„) p Î¼ :=
  memLp_stoppedValue_of_mem_finset hÏ„ hu fun Ï‰ => Finset.mem_Iic.mpr (hbdd Ï‰)

theorem integrable_stoppedValue_of_mem_finset (hÏ„ : IsStoppingTime â„± Ï„)
    (hu : âˆ€ n, Integrable (u n) Î¼) {s : Finset Î¹} (hbdd : âˆ€ Ï‰, Ï„ Ï‰ âˆˆ s) :
    Integrable (stoppedValue u Ï„) Î¼ := by
  simp_rw [â† memLp_one_iff_integrable] at hu âŠ¢
  exact memLp_stoppedValue_of_mem_finset hÏ„ hu hbdd

variable (Î¹)

theorem integrable_stoppedValue [LocallyFiniteOrderBot Î¹] (hÏ„ : IsStoppingTime â„± Ï„)
    (hu : âˆ€ n, Integrable (u n) Î¼) {N : Î¹} (hbdd : âˆ€ Ï‰, Ï„ Ï‰ â‰¤ N) :
    Integrable (stoppedValue u Ï„) Î¼ :=
  integrable_stoppedValue_of_mem_finset hÏ„ hu fun Ï‰ => Finset.mem_Iic.mpr (hbdd Ï‰)

end StoppedValue

section StoppedProcess

variable [LinearOrder Î¹] [TopologicalSpace Î¹] [OrderTopology Î¹] [FirstCountableTopology Î¹]
  {â„± : Filtration Î¹ m} [NormedAddCommGroup E]

theorem memLp_stoppedProcess_of_mem_finset (hÏ„ : IsStoppingTime â„± Ï„) (hu : âˆ€ n, MemLp (u n) p Î¼)
    (n : Î¹) {s : Finset Î¹} (hbdd : âˆ€ Ï‰, Ï„ Ï‰ < n â†’ Ï„ Ï‰ âˆˆ s) : MemLp (stoppedProcess u Ï„ n) p Î¼ := by
  rw [stoppedProcess_eq_of_mem_finset n hbdd]
  refine MemLp.add ?_ ?_
  Â· exact MemLp.indicator (â„±.le n {a : Î© | n â‰¤ Ï„ a} (hÏ„.measurableSet_ge n)) (hu n)
  Â· suffices MemLp (fun Ï‰ => âˆ‘ i âˆˆ s with i < n, {a : Î© | Ï„ a = i}.indicator (u i) Ï‰) p Î¼ by
      convert this using 1; ext1 Ï‰; simp only [Finset.sum_apply]
    refine memLp_finset_sum _ fun i _ => MemLp.indicator ?_ (hu i)
    exact â„±.le i {a : Î© | Ï„ a = i} (hÏ„.measurableSet_eq i)

theorem memLp_stoppedProcess [LocallyFiniteOrderBot Î¹] (hÏ„ : IsStoppingTime â„± Ï„)
    (hu : âˆ€ n, MemLp (u n) p Î¼) (n : Î¹) : MemLp (stoppedProcess u Ï„ n) p Î¼ :=
  memLp_stoppedProcess_of_mem_finset hÏ„ hu n fun _ h => Finset.mem_Iio.mpr h

theorem integrable_stoppedProcess_of_mem_finset (hÏ„ : IsStoppingTime â„± Ï„)
    (hu : âˆ€ n, Integrable (u n) Î¼) (n : Î¹) {s : Finset Î¹} (hbdd : âˆ€ Ï‰, Ï„ Ï‰ < n â†’ Ï„ Ï‰ âˆˆ s) :
    Integrable (stoppedProcess u Ï„ n) Î¼ := by
  simp_rw [â† memLp_one_iff_integrable] at hu âŠ¢
  exact memLp_stoppedProcess_of_mem_finset hÏ„ hu n hbdd

theorem integrable_stoppedProcess [LocallyFiniteOrderBot Î¹] (hÏ„ : IsStoppingTime â„± Ï„)
    (hu : âˆ€ n, Integrable (u n) Î¼) (n : Î¹) : Integrable (stoppedProcess u Ï„ n) Î¼ :=
  integrable_stoppedProcess_of_mem_finset hÏ„ hu n fun _ h => Finset.mem_Iio.mpr h

end StoppedProcess

end StoppedValueOfMemFinset

section AdaptedStoppedProcess

variable [TopologicalSpace Î²] [PseudoMetrizableSpace Î²] [LinearOrder Î¹] [TopologicalSpace Î¹]
  [SecondCountableTopology Î¹] [OrderTopology Î¹] [MeasurableSpace Î¹] [BorelSpace Î¹]
  {f : Filtration Î¹ m} {u : Î¹ â†’ Î© â†’ Î²} {Ï„ : Î© â†’ Î¹}

/-- The stopped process of an adapted process with continuous paths is adapted. -/
theorem Adapted.stoppedProcess [MetrizableSpace Î¹] (hu : Adapted f u)
    (hu_cont : âˆ€ Ï‰, Continuous fun i => u i Ï‰) (hÏ„ : IsStoppingTime f Ï„) :
    Adapted f (stoppedProcess u Ï„) :=
  ((hu.progMeasurable_of_continuous hu_cont).stoppedProcess hÏ„).adapted

/-- If the indexing order has the discrete topology, then the stopped process of an adapted process
is adapted. -/
theorem Adapted.stoppedProcess_of_discrete [DiscreteTopology Î¹] (hu : Adapted f u)
    (hÏ„ : IsStoppingTime f Ï„) : Adapted f (MeasureTheory.stoppedProcess u Ï„) :=
  (hu.progMeasurable_of_discrete.stoppedProcess hÏ„).adapted

theorem Adapted.stronglyMeasurable_stoppedProcess [MetrizableSpace Î¹] (hu : Adapted f u)
    (hu_cont : âˆ€ Ï‰, Continuous fun i => u i Ï‰) (hÏ„ : IsStoppingTime f Ï„) (n : Î¹) :
    StronglyMeasurable (MeasureTheory.stoppedProcess u Ï„ n) :=
  (hu.progMeasurable_of_continuous hu_cont).stronglyMeasurable_stoppedProcess hÏ„ n

theorem Adapted.stronglyMeasurable_stoppedProcess_of_discrete [DiscreteTopology Î¹]
    (hu : Adapted f u) (hÏ„ : IsStoppingTime f Ï„) (n : Î¹) :
    StronglyMeasurable (MeasureTheory.stoppedProcess u Ï„ n) :=
  hu.progMeasurable_of_discrete.stronglyMeasurable_stoppedProcess hÏ„ n

end AdaptedStoppedProcess

section Nat

/-! ### Filtrations indexed by `â„•` -/


open Filtration

variable {u : â„• â†’ Î© â†’ Î²} {Ï„ Ï€ : Î© â†’ â„•}

theorem stoppedValue_sub_eq_sum [AddCommGroup Î²] (hle : Ï„ â‰¤ Ï€) :
    stoppedValue u Ï€ - stoppedValue u Ï„ = fun Ï‰ =>
      (âˆ‘ i âˆˆ Finset.Ico (Ï„ Ï‰) (Ï€ Ï‰), (u (i + 1) - u i)) Ï‰ := by
  ext Ï‰
  rw [Finset.sum_Ico_eq_sub _ (hle Ï‰), Finset.sum_range_sub, Finset.sum_range_sub]
  simp [stoppedValue]

theorem stoppedValue_sub_eq_sum' [AddCommGroup Î²] (hle : Ï„ â‰¤ Ï€) {N : â„•} (hbdd : âˆ€ Ï‰, Ï€ Ï‰ â‰¤ N) :
    stoppedValue u Ï€ - stoppedValue u Ï„ = fun Ï‰ =>
      (âˆ‘ i âˆˆ Finset.range (N + 1), Set.indicator {Ï‰ | Ï„ Ï‰ â‰¤ i âˆ§ i < Ï€ Ï‰} (u (i + 1) - u i)) Ï‰ := by
  rw [stoppedValue_sub_eq_sum hle]
  ext Ï‰
  simp only [Finset.sum_apply, Finset.sum_indicator_eq_sum_filter]
  refine Finset.sum_congr ?_ fun _ _ => rfl
  ext i
  simp only [Finset.mem_filter, Set.mem_setOf_eq, Finset.mem_range, Finset.mem_Ico]
  exact âŸ¨fun h => âŸ¨lt_trans h.2 (Nat.lt_succ_iff.2 <| hbdd _), hâŸ©, fun h => h.2âŸ©

section AddCommMonoid

variable [AddCommMonoid Î²]

theorem stoppedValue_eq {N : â„•} (hbdd : âˆ€ Ï‰, Ï„ Ï‰ â‰¤ N) : stoppedValue u Ï„ = fun x =>
    (âˆ‘ i âˆˆ Finset.range (N + 1), Set.indicator {Ï‰ | Ï„ Ï‰ = i} (u i)) x :=
  stoppedValue_eq_of_mem_finset fun Ï‰ => Finset.mem_range_succ_iff.mpr (hbdd Ï‰)

theorem stoppedProcess_eq (n : â„•) : stoppedProcess u Ï„ n = Set.indicator {a | n â‰¤ Ï„ a} (u n) +
    âˆ‘ i âˆˆ Finset.range n, Set.indicator {Ï‰ | Ï„ Ï‰ = i} (u i) := by
  rw [stoppedProcess_eq'' n]
  congr with i
  rw [Finset.mem_Iio, Finset.mem_range]

theorem stoppedProcess_eq' (n : â„•) : stoppedProcess u Ï„ n = Set.indicator {a | n + 1 â‰¤ Ï„ a} (u n) +
    âˆ‘ i âˆˆ Finset.range (n + 1), Set.indicator {a | Ï„ a = i} (u i) := by
  have : {a | n â‰¤ Ï„ a}.indicator (u n) =
      {a | n + 1 â‰¤ Ï„ a}.indicator (u n) + {a | Ï„ a = n}.indicator (u n) := by
    ext x
    rw [add_comm, Pi.add_apply, â† Set.indicator_union_of_notMem_inter]
    Â· simp_rw [@eq_comm _ _ n, @le_iff_eq_or_lt _ _ n, Nat.succ_le_iff, Set.setOf_or]
    Â· rintro âŸ¨hâ‚, hâ‚‚âŸ©
      rw [Set.mem_setOf] at hâ‚ hâ‚‚
      exact (Nat.succ_le_iff.1 hâ‚‚).ne hâ‚.symm
  rw [stoppedProcess_eq, this, Finset.sum_range_succ_comm, â† add_assoc]

end AddCommMonoid

end Nat

section PiecewiseConst

variable [Preorder Î¹] {ğ’¢ : Filtration Î¹ m} {Ï„ Î· : Î© â†’ Î¹} {i j : Î¹} {s : Set Î©}
  [DecidablePred (Â· âˆˆ s)]

/-- Given stopping times `Ï„` and `Î·` which are bounded below, `Set.piecewise s Ï„ Î·` is also
a stopping time with respect to the same filtration. -/
theorem IsStoppingTime.piecewise_of_le (hÏ„_st : IsStoppingTime ğ’¢ Ï„) (hÎ·_st : IsStoppingTime ğ’¢ Î·)
    (hÏ„ : âˆ€ Ï‰, i â‰¤ Ï„ Ï‰) (hÎ· : âˆ€ Ï‰, i â‰¤ Î· Ï‰) (hs : MeasurableSet[ğ’¢ i] s) :
    IsStoppingTime ğ’¢ (s.piecewise Ï„ Î·) := by
  intro n
  have : {Ï‰ | s.piecewise Ï„ Î· Ï‰ â‰¤ n} = s âˆ© {Ï‰ | Ï„ Ï‰ â‰¤ n} âˆª sá¶œ âˆ© {Ï‰ | Î· Ï‰ â‰¤ n} := by
    ext1 Ï‰
    simp only [Set.piecewise, Set.mem_setOf_eq]
    by_cases hx : Ï‰ âˆˆ s <;> simp [hx]
  rw [this]
  by_cases hin : i â‰¤ n
  Â· have hs_n : MeasurableSet[ğ’¢ n] s := ğ’¢.mono hin _ hs
    exact (hs_n.inter (hÏ„_st n)).union (hs_n.compl.inter (hÎ·_st n))
  Â· have hÏ„n : âˆ€ Ï‰, Â¬Ï„ Ï‰ â‰¤ n := fun Ï‰ hÏ„n => hin ((hÏ„ Ï‰).trans hÏ„n)
    have hÎ·n : âˆ€ Ï‰, Â¬Î· Ï‰ â‰¤ n := fun Ï‰ hÎ·n => hin ((hÎ· Ï‰).trans hÎ·n)
    simp [hÏ„n, hÎ·n, @MeasurableSet.empty _ _]

theorem isStoppingTime_piecewise_const (hij : i â‰¤ j) (hs : MeasurableSet[ğ’¢ i] s) :
    IsStoppingTime ğ’¢ (s.piecewise (fun _ => i) fun _ => j) :=
  (isStoppingTime_const ğ’¢ i).piecewise_of_le (isStoppingTime_const ğ’¢ j) (fun _ => le_rfl)
    (fun _ => hij) hs

theorem stoppedValue_piecewise_const {Î¹' : Type*} {i j : Î¹'} {f : Î¹' â†’ Î© â†’ â„} :
    stoppedValue f (s.piecewise (fun _ => i) fun _ => j) = s.piecewise (f i) (f j) := by
  ext Ï‰; rw [stoppedValue]; by_cases hx : Ï‰ âˆˆ s <;> simp [hx]

theorem stoppedValue_piecewise_const' {Î¹' : Type*} {i j : Î¹'} {f : Î¹' â†’ Î© â†’ â„} :
    stoppedValue f (s.piecewise (fun _ => i) fun _ => j) =
    s.indicator (f i) + sá¶œ.indicator (f j) := by
  ext Ï‰; rw [stoppedValue]; by_cases hx : Ï‰ âˆˆ s <;> simp [hx]

end PiecewiseConst

section Condexp

/-! ### Conditional expectation with respect to the Ïƒ-algebra generated by a stopping time -/


variable [LinearOrder Î¹] {Î¼ : Measure Î©} {â„± : Filtration Î¹ m} {Ï„ Ïƒ : Î© â†’ Î¹} {E : Type*}
  [NormedAddCommGroup E] [NormedSpace â„ E] [CompleteSpace E] {f : Î© â†’ E}

theorem condExp_stopping_time_ae_eq_restrict_eq_of_countable_range [SigmaFiniteFiltration Î¼ â„±]
    (hÏ„ : IsStoppingTime â„± Ï„) (h_countable : (Set.range Ï„).Countable)
    [SigmaFinite (Î¼.trim (hÏ„.measurableSpace_le_of_countable_range h_countable))] (i : Î¹) :
    Î¼[f|hÏ„.measurableSpace] =áµ[Î¼.restrict {x | Ï„ x = i}] Î¼[f|â„± i] := by
  refine condExp_ae_eq_restrict_of_measurableSpace_eq_on
    (hÏ„.measurableSpace_le_of_countable_range h_countable) (â„±.le i)
    (hÏ„.measurableSet_eq_of_countable_range' h_countable i) fun t => ?_
  rw [Set.inter_comm _ t, IsStoppingTime.measurableSet_inter_eq_iff]

theorem condExp_stopping_time_ae_eq_restrict_eq_of_countable [Countable Î¹]
    [SigmaFiniteFiltration Î¼ â„±] (hÏ„ : IsStoppingTime â„± Ï„)
    [SigmaFinite (Î¼.trim hÏ„.measurableSpace_le_of_countable)] (i : Î¹) :
    Î¼[f|hÏ„.measurableSpace] =áµ[Î¼.restrict {x | Ï„ x = i}] Î¼[f|â„± i] :=
  condExp_stopping_time_ae_eq_restrict_eq_of_countable_range hÏ„ (Set.to_countable _) i

variable [(Filter.atTop : Filter Î¹).IsCountablyGenerated]

theorem condExp_min_stopping_time_ae_eq_restrict_le_const (hÏ„ : IsStoppingTime â„± Ï„) (i : Î¹)
    [SigmaFinite (Î¼.trim (hÏ„.min_const i).measurableSpace_le)] :
    Î¼[f|(hÏ„.min_const i).measurableSpace] =áµ[Î¼.restrict {x | Ï„ x â‰¤ i}] Î¼[f|hÏ„.measurableSpace] := by
  have : SigmaFinite (Î¼.trim hÏ„.measurableSpace_le) :=
    haveI h_le : (hÏ„.min_const i).measurableSpace â‰¤ hÏ„.measurableSpace := by
      rw [IsStoppingTime.measurableSpace_min_const]
      exact inf_le_left
    sigmaFiniteTrim_mono _ h_le
  refine (condExp_ae_eq_restrict_of_measurableSpace_eq_on hÏ„.measurableSpace_le
    (hÏ„.min_const i).measurableSpace_le (hÏ„.measurableSet_le' i) fun t => ?_).symm
  rw [Set.inter_comm _ t, hÏ„.measurableSet_inter_le_const_iff]

variable [TopologicalSpace Î¹] [OrderTopology Î¹]

theorem condExp_stopping_time_ae_eq_restrict_eq [FirstCountableTopology Î¹]
    [SigmaFiniteFiltration Î¼ â„±] (hÏ„ : IsStoppingTime â„± Ï„)
    [SigmaFinite (Î¼.trim hÏ„.measurableSpace_le)] (i : Î¹) :
    Î¼[f|hÏ„.measurableSpace] =áµ[Î¼.restrict {x | Ï„ x = i}] Î¼[f|â„± i] := by
  refine condExp_ae_eq_restrict_of_measurableSpace_eq_on hÏ„.measurableSpace_le (â„±.le i)
    (hÏ„.measurableSet_eq' i) fun t => ?_
  rw [Set.inter_comm _ t, IsStoppingTime.measurableSet_inter_eq_iff]

theorem condExp_min_stopping_time_ae_eq_restrict_le [MeasurableSpace Î¹] [SecondCountableTopology Î¹]
    [BorelSpace Î¹] (hÏ„ : IsStoppingTime â„± Ï„) (hÏƒ : IsStoppingTime â„± Ïƒ)
    [SigmaFinite (Î¼.trim (hÏ„.min hÏƒ).measurableSpace_le)] :
    Î¼[f|(hÏ„.min hÏƒ).measurableSpace] =áµ[Î¼.restrict {x | Ï„ x â‰¤ Ïƒ x}] Î¼[f|hÏ„.measurableSpace] := by
  have : SigmaFinite (Î¼.trim hÏ„.measurableSpace_le) :=
    haveI h_le : (hÏ„.min hÏƒ).measurableSpace â‰¤ hÏ„.measurableSpace := by
      rw [IsStoppingTime.measurableSpace_min]
      Â· exact inf_le_left
      Â· simp_all only
    sigmaFiniteTrim_mono _ h_le
  refine (condExp_ae_eq_restrict_of_measurableSpace_eq_on hÏ„.measurableSpace_le
    (hÏ„.min hÏƒ).measurableSpace_le (hÏ„.measurableSet_le_stopping_time hÏƒ) fun t => ?_).symm
  rw [Set.inter_comm _ t, IsStoppingTime.measurableSet_inter_le_iff]; simp_all only

end Condexp

end MeasureTheory
