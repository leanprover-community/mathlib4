/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Jonas Bayer
-/

import Mathlib.MeasureTheory.Constructions.Projective
import Mathlib.Probability.IdentDistrib

/-!
# Finite dimensional distributions of a stochastic process

For a stochastic process `X : T â†’ Î© â†’ ğ“§` and a finite measure `P` on `Î©`, the law of the process is
`P.map (fun Ï‰ â†¦ (X Â· Ï‰))`, and its finite dimensional distributions are
`P.map (fun Ï‰ â†¦ I.restrict (X Â· Ï‰))` for `I : Finset T`.

We show that two stochastic processes have the same laws if and only if they have the same
finite dimensional distributions.

## Main statements

* `map_eq_iff_forall_finset_map_restrict_eq`: two processes have the same law if and only if
  their finite dimensional distributions are equal.
* `identDistrib_iff_forall_finset_identDistrib`: same statement, but stated in terms of
  `IdentDistrib`.
* `map_restrict_eq_of_forall_ae_eq`: if two processes are modifications of each other, then
  their finite dimensional distributions are equal.
* `map_eq_of_forall_ae_eq`: if two processes are modifications of each other, then they have the
  same law.

-/

open MeasureTheory

namespace ProbabilityTheory

variable {T Î© : Type*} {ğ“§ : T â†’ Type*} {mÎ© : MeasurableSpace Î©} {mÎ± : âˆ€ t, MeasurableSpace (ğ“§ t)}
  {X Y : (t : T) â†’ Î© â†’ ğ“§ t} {P : Measure Î©}

/-- The finite dimensional distributions of a stochastic process are a projective measure family. -/
lemma isProjectiveMeasureFamily_map_restrict (hX : âˆ€ t, AEMeasurable (X t) P) :
    IsProjectiveMeasureFamily (fun I â†¦ P.map (fun Ï‰ â†¦ I.restrict (X Â· Ï‰))) := by
  intro I J hJI
  rw [AEMeasurable.map_map_of_aemeasurable (Finset.measurable_restrictâ‚‚ _).aemeasurable]
  Â· rfl
  Â· exact aemeasurable_pi_lambda _ fun _ â†¦ hX _

/-- The projective limit of the finite dimensional distributions of a stochastic process is the law
of the process. -/
lemma isProjectiveLimit_map (hX : AEMeasurable (fun Ï‰ â†¦ (X Â· Ï‰)) P) :
    IsProjectiveLimit (P.map (fun Ï‰ â†¦ (X Â· Ï‰))) (fun I â†¦ P.map (fun Ï‰ â†¦ I.restrict (X Â· Ï‰))) := by
  intro I
  rw [AEMeasurable.map_map_of_aemeasurable (Finset.measurable_restrict _).aemeasurable hX]
  rfl

/-- Two stochastic processes have same law iff they have the same
finite dimensional distributions. -/
lemma map_eq_iff_forall_finset_map_restrict_eq [IsFiniteMeasure P]
    (hX : AEMeasurable (fun Ï‰ â†¦ (X Â· Ï‰)) P) (hY : AEMeasurable (fun Ï‰ â†¦ (Y Â· Ï‰)) P) :
    P.map (fun Ï‰ â†¦ (X Â· Ï‰)) = P.map (fun Ï‰ â†¦ (Y Â· Ï‰))
    â†” âˆ€ I : Finset T, P.map (fun Ï‰ â†¦ I.restrict (X Â· Ï‰)) = P.map (fun Ï‰ â†¦ I.restrict (Y Â· Ï‰)) := by
  refine âŸ¨fun h I â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· have hX' : P.map (fun Ï‰ â†¦ I.restrict (X Â· Ï‰)) = (P.map (fun Ï‰ â†¦ (X Â· Ï‰))).map I.restrict := by
      rw [AEMeasurable.map_map_of_aemeasurable (by fun_prop) hX]
      rfl
    have hY' : P.map (fun Ï‰ â†¦ I.restrict (Y Â· Ï‰)) = (P.map (fun Ï‰ â†¦ (Y Â· Ï‰))).map I.restrict := by
      rw [AEMeasurable.map_map_of_aemeasurable (by fun_prop) hY]
      rfl
    rw [hX', hY', h]
  Â· have hX' := isProjectiveLimit_map hX
    simp_rw [h] at hX'
    exact hX'.unique (isProjectiveLimit_map hY)

/-- Two stochastic processes are identically distributed iff they have the same
finite dimensional distributions. -/
lemma identDistrib_iff_forall_finset_identDistrib [IsFiniteMeasure P]
    (hX : AEMeasurable (fun Ï‰ â†¦ (X Â· Ï‰)) P) (hY : AEMeasurable (fun Ï‰ â†¦ (Y Â· Ï‰)) P) :
    IdentDistrib (fun Ï‰ â†¦ (X Â· Ï‰)) (fun Ï‰ â†¦ (Y Â· Ï‰)) P P
      â†” âˆ€ I : Finset T,
        IdentDistrib (fun Ï‰ â†¦ I.restrict (X Â· Ï‰)) (fun Ï‰ â†¦ I.restrict (Y Â· Ï‰)) P P := by
  refine âŸ¨fun h I â†¦ âŸ¨?_, ?_, ?_âŸ©, fun h â†¦ âŸ¨hX, hY, ?_âŸ©âŸ©
  Â· exact (Finset.measurable_restrict _).comp_aemeasurable hX
  Â· exact (Finset.measurable_restrict _).comp_aemeasurable hY
  Â· exact (map_eq_iff_forall_finset_map_restrict_eq hX hY).mp h.map_eq I
  Â· exact (map_eq_iff_forall_finset_map_restrict_eq hX hY).mpr (fun I â†¦ (h I).map_eq)

/-- If two processes are modifications of each other, then they have the same finite dimensional
distributions. -/
lemma map_restrict_eq_of_forall_ae_eq (h : âˆ€ t, X t =áµ[P] Y t) (I : Finset T) :
    P.map (fun Ï‰ â†¦ I.restrict (X Â· Ï‰)) = P.map (fun Ï‰ â†¦ I.restrict (Y Â· Ï‰)) := by
  have h' : âˆ€áµ Ï‰ âˆ‚P, âˆ€ (i : I), X i Ï‰ = Y i Ï‰ := by
    rw [MeasureTheory.ae_all_iff]
    exact fun i â†¦ h i
  refine Measure.map_congr ?_
  filter_upwards [h'] with Ï‰ h using funext h

/-- If two processes are modifications of each other, then they have the same distribution. -/
lemma map_eq_of_forall_ae_eq [IsFiniteMeasure P]
    (hX : AEMeasurable (fun Ï‰ â†¦ (X Â· Ï‰)) P) (hY : AEMeasurable (fun Ï‰ â†¦ (Y Â· Ï‰)) P)
    (h : âˆ€ t, X t =áµ[P] Y t) :
    P.map (fun Ï‰ â†¦ (X Â· Ï‰)) = P.map (fun Ï‰ â†¦ (Y Â· Ï‰)) := by
  rw [map_eq_iff_forall_finset_map_restrict_eq hX hY]
  exact fun I â†¦ map_restrict_eq_of_forall_ae_eq h I

end ProbabilityTheory
