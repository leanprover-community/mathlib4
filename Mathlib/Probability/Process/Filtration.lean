/-
Copyright (c) 2021 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying, RÃ©my Degenne
-/
module

public import Mathlib.MeasureTheory.Constructions.Cylinders
public import Mathlib.MeasureTheory.Function.ConditionalExpectation.Real
public import Mathlib.MeasureTheory.MeasurableSpace.PreorderRestrict

/-!
# Filtrations

This file defines filtrations of a measurable space and Ïƒ-finite filtrations.

## Main definitions

* `MeasureTheory.Filtration`: a filtration on a measurable space. That is, a monotone sequence of
  sub-Ïƒ-algebras.
* `MeasureTheory.SigmaFiniteFiltration`: a filtration `f` is Ïƒ-finite with respect to a measure
  `Î¼` if for all `i`, `Î¼.trim (f.le i)` is Ïƒ-finite.
* `MeasureTheory.Filtration.natural`: the smallest filtration that makes a process adapted. That
  notion `adapted` is not defined yet in this file. See `MeasureTheory.adapted`.
* `MeasureTheory.Filtration.rightCont`: the right-continuation of a filtration.
* `MeasureTheory.Filtration.IsRightContinuous`: a filtration is right-continuous if it is equal
  to its right-continuation.

## Main results

* `MeasureTheory.Filtration.instCompleteLattice`: filtrations are a complete lattice.

## Tags

filtration, stochastic process

-/

@[expose] public section


open Filter Order TopologicalSpace

open scoped MeasureTheory NNReal ENNReal Topology

namespace MeasureTheory

/-- A `Filtration` on a measurable space `Î©` with Ïƒ-algebra `m` is a monotone
sequence of sub-Ïƒ-algebras of `m`. -/
structure Filtration {Î© : Type*} (Î¹ : Type*) [Preorder Î¹] (m : MeasurableSpace Î©) where
  /-- The sequence of sub-Ïƒ-algebras of `m` -/
  seq : Î¹ â†’ MeasurableSpace Î©
  mono' : Monotone seq
  le' : âˆ€ i : Î¹, seq i â‰¤ m

attribute [coe] Filtration.seq

variable {Î© Î¹ : Type*} {m : MeasurableSpace Î©}

instance [Preorder Î¹] : CoeFun (Filtration Î¹ m) fun _ => Î¹ â†’ MeasurableSpace Î© :=
  âŸ¨fun f => f.seqâŸ©

namespace Filtration

variable [Preorder Î¹]

protected theorem mono {i j : Î¹} (f : Filtration Î¹ m) (hij : i â‰¤ j) : f i â‰¤ f j :=
  f.mono' hij

protected theorem le (f : Filtration Î¹ m) (i : Î¹) : f i â‰¤ m :=
  f.le' i

@[ext]
protected theorem ext {f g : Filtration Î¹ m} (h : (f : Î¹ â†’ MeasurableSpace Î©) = g) : f = g := by
  cases f; cases g; congr

variable (Î¹) in
/-- The constant filtration which is equal to `m` for all `i : Î¹`. -/
def const (m' : MeasurableSpace Î©) (hm' : m' â‰¤ m) : Filtration Î¹ m :=
  âŸ¨fun _ => m', monotone_const, fun _ => hm'âŸ©

@[simp]
theorem const_apply {m' : MeasurableSpace Î©} {hm' : m' â‰¤ m} (i : Î¹) : const Î¹ m' hm' i = m' :=
  rfl

instance : Inhabited (Filtration Î¹ m) :=
  âŸ¨const Î¹ m le_rflâŸ©

instance : LE (Filtration Î¹ m) :=
  âŸ¨fun f g => âˆ€ i, f i â‰¤ g iâŸ©

instance : Bot (Filtration Î¹ m) :=
  âŸ¨const Î¹ âŠ¥ bot_leâŸ©

instance : Top (Filtration Î¹ m) :=
  âŸ¨const Î¹ m le_rflâŸ©

instance : Max (Filtration Î¹ m) :=
  âŸ¨fun f g =>
    { seq := fun i => f i âŠ” g i
      mono' := fun _ _ hij =>
        sup_le ((f.mono hij).trans le_sup_left) ((g.mono hij).trans le_sup_right)
      le' := fun i => sup_le (f.le i) (g.le i) }âŸ©

@[norm_cast]
theorem coeFn_sup {f g : Filtration Î¹ m} : â‡‘(f âŠ” g) = â‡‘f âŠ” â‡‘g :=
  rfl

instance : Min (Filtration Î¹ m) :=
  âŸ¨fun f g =>
    { seq := fun i => f i âŠ“ g i
      mono' := fun _ _ hij =>
        le_inf (inf_le_left.trans (f.mono hij)) (inf_le_right.trans (g.mono hij))
      le' := fun i => inf_le_left.trans (f.le i) }âŸ©

@[norm_cast]
theorem coeFn_inf {f g : Filtration Î¹ m} : â‡‘(f âŠ“ g) = â‡‘f âŠ“ â‡‘g :=
  rfl

instance : SupSet (Filtration Î¹ m) :=
  âŸ¨fun s =>
    { seq := fun i => sSup ((fun f : Filtration Î¹ m => f i) '' s)
      mono' := fun i j hij => by
        refine sSup_le fun m' hm' => ?_
        rw [Set.mem_image] at hm'
        obtain âŸ¨f, hf_mem, hfm'âŸ© := hm'
        rw [â† hfm']
        refine (f.mono hij).trans ?_
        have hfj_mem : f j âˆˆ (fun g : Filtration Î¹ m => g j) '' s := âŸ¨f, hf_mem, rflâŸ©
        exact le_sSup hfj_mem
      le' := fun i => by
        refine sSup_le fun m' hm' => ?_
        rw [Set.mem_image] at hm'
        obtain âŸ¨f, _, hfm'âŸ© := hm'
        rw [â† hfm']
        exact f.le i }âŸ©

theorem sSup_def (s : Set (Filtration Î¹ m)) (i : Î¹) :
    sSup s i = sSup ((fun f : Filtration Î¹ m => f i) '' s) :=
  rfl

open scoped Classical in
noncomputable instance : InfSet (Filtration Î¹ m) :=
  âŸ¨fun s =>
    { seq := fun i => if Set.Nonempty s then sInf ((fun f : Filtration Î¹ m => f i) '' s) else m
      mono' := fun i j hij => by
        by_cases h_nonempty : Set.Nonempty s
        swap; Â· simp only [h_nonempty, if_false, le_refl]
        simp only [h_nonempty, if_true, le_sInf_iff, Set.mem_image, forall_exists_index, and_imp,
          forall_apply_eq_imp_iffâ‚‚]
        refine fun f hf_mem => le_trans ?_ (f.mono hij)
        have hfi_mem : f i âˆˆ (fun g : Filtration Î¹ m => g i) '' s := âŸ¨f, hf_mem, rflâŸ©
        exact sInf_le hfi_mem
      le' := fun i => by
        by_cases h_nonempty : Set.Nonempty s
        swap; Â· simp only [h_nonempty, if_false, le_refl]
        simp only [h_nonempty, if_true]
        obtain âŸ¨f, hf_memâŸ© := h_nonempty
        exact le_trans (sInf_le âŸ¨f, hf_mem, rflâŸ©) (f.le i) }âŸ©

open scoped Classical in
theorem sInf_def (s : Set (Filtration Î¹ m)) (i : Î¹) :
    sInf s i = if Set.Nonempty s then sInf ((fun f : Filtration Î¹ m => f i) '' s) else m :=
  rfl

noncomputable instance instCompleteLattice : CompleteLattice (Filtration Î¹ m) where
  le_refl _ _ := le_rfl
  le_trans _ _ _ h_fg h_gh i := (h_fg i).trans (h_gh i)
  le_antisymm _ _ h_fg h_gf := Filtration.ext <| funext fun i => (h_fg i).antisymm (h_gf i)
  sup := (Â· âŠ” Â·)
  le_sup_left _ _ _ := le_sup_left
  le_sup_right _ _ _ := le_sup_right
  sup_le _ _ _ h_fh h_gh i := sup_le (h_fh i) (h_gh _)
  inf := (Â· âŠ“ Â·)
  inf_le_left _ _ _ := inf_le_left
  inf_le_right _ _ _ := inf_le_right
  le_inf _ _ _ h_fg h_fh i := le_inf (h_fg i) (h_fh i)
  le_sSup _ f hf_mem _ := le_sSup âŸ¨f, hf_mem, rflâŸ©
  sSup_le s f h_forall i :=
    sSup_le fun m' hm' => by
      obtain âŸ¨g, hg_mem, hfm'âŸ© := hm'
      rw [â† hfm']
      exact h_forall g hg_mem i
  sInf_le s f hf_mem i := by
    have hs : s.Nonempty := âŸ¨f, hf_memâŸ©
    simp only [sInf_def, hs, if_true]
    exact sInf_le âŸ¨f, hf_mem, rflâŸ©
  le_sInf s f h_forall i := by
    by_cases hs : s.Nonempty
    swap; Â· simp only [sInf_def, hs, if_false]; exact f.le i
    simp only [sInf_def, hs, if_true, le_sInf_iff, Set.mem_image, forall_exists_index, and_imp,
      forall_apply_eq_imp_iffâ‚‚]
    exact fun g hg_mem => h_forall g hg_mem i
  le_top f i := f.le' i
  bot_le _ _ := bot_le

end Filtration

theorem measurableSet_of_filtration [Preorder Î¹] {f : Filtration Î¹ m} {s : Set Î©} {i : Î¹}
    (hs : MeasurableSet[f i] s) : MeasurableSet[m] s :=
  f.le i s hs

/-- A measure is Ïƒ-finite with respect to filtration if it is Ïƒ-finite with respect
to all the sub-Ïƒ-algebra of the filtration. -/
class SigmaFiniteFiltration [Preorder Î¹] (Î¼ : Measure Î©) (f : Filtration Î¹ m) : Prop where
  SigmaFinite : âˆ€ i : Î¹, SigmaFinite (Î¼.trim (f.le i))

instance sigmaFinite_of_sigmaFiniteFiltration [Preorder Î¹] (Î¼ : Measure Î©) (f : Filtration Î¹ m)
    [hf : SigmaFiniteFiltration Î¼ f] (i : Î¹) : SigmaFinite (Î¼.trim (f.le i)) :=
  hf.SigmaFinite _

instance (priority := 100) IsFiniteMeasure.sigmaFiniteFiltration [Preorder Î¹] (Î¼ : Measure Î©)
    (f : Filtration Î¹ m) [IsFiniteMeasure Î¼] : SigmaFiniteFiltration Î¼ f :=
  âŸ¨fun n => by infer_instanceâŸ©

/-- Given an integrable function `g`, the conditional expectations of `g` with respect to a
filtration is uniformly integrable. -/
theorem Integrable.uniformIntegrable_condExp_filtration [Preorder Î¹] {Î¼ : Measure Î©}
    [IsFiniteMeasure Î¼] {f : Filtration Î¹ m} {g : Î© â†’ â„} (hg : Integrable g Î¼) :
    UniformIntegrable (fun i => Î¼[g|f i]) 1 Î¼ :=
  hg.uniformIntegrable_condExp f.le

theorem Filtration.condExp_condExp [Preorder Î¹] {E : Type*} [NormedAddCommGroup E]
    [NormedSpace â„ E] [CompleteSpace E] (f : Î© â†’ E) {Î¼ : Measure Î©} (â„± : Filtration Î¹ m)
    {i j : Î¹} (hij : i â‰¤ j) [SigmaFinite (Î¼.trim (â„±.le j))] :
    Î¼[Î¼[f|â„± j]|â„± i] =áµ[Î¼] Î¼[f|â„± i] := condExp_condExp_of_le (â„±.mono hij) (â„±.le j)

section OfSet

variable [Preorder Î¹]

/-- Given a sequence of measurable sets `(sâ‚™)`, `filtrationOfSet` is the smallest filtration
such that `sâ‚™` is measurable with respect to the `n`-th sub-Ïƒ-algebra in `filtrationOfSet`. -/
def filtrationOfSet {s : Î¹ â†’ Set Î©} (hsm : âˆ€ i, MeasurableSet (s i)) : Filtration Î¹ m where
  seq i := MeasurableSpace.generateFrom {t | âˆƒ j â‰¤ i, s j = t}
  mono' _ _ hnm := MeasurableSpace.generateFrom_mono fun _ âŸ¨k, hkâ‚, hkâ‚‚âŸ© => âŸ¨k, hkâ‚.trans hnm, hkâ‚‚âŸ©
  le' _ := MeasurableSpace.generateFrom_le fun _ âŸ¨k, _, hkâ‚‚âŸ© => hkâ‚‚ â–¸ hsm k

theorem measurableSet_filtrationOfSet {s : Î¹ â†’ Set Î©} (hsm : âˆ€ i, MeasurableSet[m] (s i)) (i : Î¹)
    {j : Î¹} (hj : j â‰¤ i) : MeasurableSet[filtrationOfSet hsm i] (s j) :=
  MeasurableSpace.measurableSet_generateFrom âŸ¨j, hj, rflâŸ©

theorem measurableSet_filtrationOfSet' {s : Î¹ â†’ Set Î©} (hsm : âˆ€ n, MeasurableSet[m] (s n))
    (i : Î¹) : MeasurableSet[filtrationOfSet hsm i] (s i) :=
  measurableSet_filtrationOfSet hsm i le_rfl

end OfSet

namespace Filtration

section IsRightContinuous

open scoped Classical in
/-- Given a filtration `ğ“•`, its **right continuation** is the filtration `ğ“•â‚Š` defined as follows:
- If `i` is isolated on the right, then `ğ“•â‚Š i := ğ“• i`;
- Otherwise, `ğ“•â‚Š i := â¨… j > i, ğ“• j`.
It is sometimes simply defined as `ğ“•â‚Š i := â¨… j > i, ğ“• j` when the index type is `â„`. In the
general case this is not ideal however. If `i` is maximal for instance, then `ğ“•â‚Š i = âŠ¤`, which
is inconvenient because `ğ“•â‚Š` is not  a `Filtration Î¹ m` anymore. If the index type
is discrete (such as `â„•`), then we would have `ğ“• = ğ“•â‚Š` (i.e. `ğ“•` is right-continuous) only if
`ğ“•` is constant.

To avoid requiring a `TopologicalSpace` instance on `Î¹` in the definition, we endow `Î¹` with
the order topology `Preorder.topology` inside the definition. Say you write a statement about
`ğ“•â‚Š` which does not require a `TopologicalSpace` structure on `Î¹`,
but you wish to use a statement which requires a topology (such as `rightCont_def`).
Then you can endow `Î¹` with
the order topology by writing
```lean
  letI := Preorder.topology Î¹
  haveI : OrderTopology Î¹ := âŸ¨rflâŸ©
``` -/
noncomputable def rightCont [PartialOrder Î¹] (ğ“• : Filtration Î¹ m) : Filtration Î¹ m :=
  letI : TopologicalSpace Î¹ := Preorder.topology Î¹
  { seq i := if (ğ“[>] i).NeBot then â¨… j > i, ğ“• j else ğ“• i
    mono' i j hij := by
      simp only [gt_iff_lt]
      split_ifs with hi hj hj
      Â· exact le_iInfâ‚‚ fun k hkj â†¦ iInfâ‚‚_le k (hij.trans_lt hkj)
      Â· obtain rfl | hj := eq_or_ne j i
        Â· contradiction
        Â· exact iInfâ‚‚_le j (lt_of_le_of_ne hij hj.symm)
      Â· exact le_iInfâ‚‚ fun k hk â†¦ ğ“•.mono (hij.trans hk.le)
      Â· exact ğ“•.mono hij
    le' i := by
      split_ifs with hi
      Â· obtain âŸ¨j, hjâŸ© := (frequently_gt_nhds i).exists
        exact iInfâ‚‚_le_of_le j hj (ğ“•.le j)
      Â· exact ğ“•.le i }

@[inherit_doc] scoped postfix:max "â‚Š" => rightCont

open scoped Classical in
lemma rightCont_def [PartialOrder Î¹] [TopologicalSpace Î¹] [OrderTopology Î¹]
    (ğ“• : Filtration Î¹ m) (i : Î¹) :
    ğ“•â‚Š i = if (ğ“[>] i).NeBot then â¨… j > i, ğ“• j else ğ“• i := by
  simp only [rightCont, OrderTopology.topology_eq_generate_intervals]

lemma rightCont_eq_of_nhdsGT_eq_bot [PartialOrder Î¹] [TopologicalSpace Î¹] [OrderTopology Î¹]
    (ğ“• : Filtration Î¹ m) {i : Î¹} (hi : ğ“[>] i = âŠ¥) :
    ğ“•â‚Š i = ğ“• i := by
  rw [rightCont_def, hi, neBot_iff, ne_self_iff_false, if_false]

/-- If the index type is a `SuccOrder`, then `ğ“•â‚Š = ğ“•`. -/
lemma rightCont_eq_self [LinearOrder Î¹] [SuccOrder Î¹] (ğ“• : Filtration Î¹ m) :
    ğ“•â‚Š = ğ“• := by
  letI := Preorder.topology Î¹; haveI : OrderTopology Î¹ := âŸ¨rflâŸ©
  ext _
  rw [rightCont_eq_of_nhdsGT_eq_bot _ SuccOrder.nhdsGT]

lemma rightCont_eq_of_isMax [PartialOrder Î¹] (ğ“• : Filtration Î¹ m) {i : Î¹} (hi : IsMax i) :
    ğ“•â‚Š i = ğ“• i := by
  letI := Preorder.topology Î¹; haveI : OrderTopology Î¹ := âŸ¨rflâŸ©
  exact rightCont_eq_of_nhdsGT_eq_bot _ (hi.Ioi_eq â–¸ nhdsWithin_empty i)

lemma rightCont_eq_of_exists_gt [LinearOrder Î¹] (ğ“• : Filtration Î¹ m) {i : Î¹}
    (hi : âˆƒ j > i, Set.Ioo i j = âˆ…) :
    ğ“•â‚Š i = ğ“• i := by
  letI := Preorder.topology Î¹; haveI : OrderTopology Î¹ := âŸ¨rflâŸ©
  obtain âŸ¨j, hij, hIooâŸ© := hi
  have hcov : i â‹– j := covBy_iff_Ioo_eq.mpr âŸ¨hij, hIooâŸ©
  exact rightCont_eq_of_nhdsGT_eq_bot _ <| CovBy.nhdsGT hcov

/-- If `i` is not isolated on the right, then `ğ“•â‚Š i = â¨… j > i, ğ“• j`. This is for instance the case
when `Î¹` is a densely ordered linear order with no maximal elements and equipped with the order
topology, see `rightCont_eq`. -/
lemma rightCont_eq_of_neBot_nhdsGT [PartialOrder Î¹] [TopologicalSpace Î¹] [OrderTopology Î¹]
    (ğ“• : Filtration Î¹ m) (i : Î¹) [(ğ“[>] i).NeBot] :
    ğ“•â‚Š i = â¨… j > i, ğ“• j := by
  rw [rightCont_def, if_pos â€¹(ğ“[>] i).NeBotâ€º]

lemma rightCont_eq_of_not_isMax [LinearOrder Î¹] [DenselyOrdered Î¹]
    (ğ“• : Filtration Î¹ m) {i : Î¹} (hi : Â¬IsMax i) :
    ğ“•â‚Š i = â¨… j > i, ğ“• j := by
  letI := Preorder.topology Î¹; haveI : OrderTopology Î¹ := âŸ¨rflâŸ©
  have : (ğ“[>] i).NeBot := nhdsGT_neBot_of_exists_gt (not_isMax_iff.mp hi)
  exact rightCont_eq_of_neBot_nhdsGT _ _

/-- If `Î¹` is a densely ordered linear order with no maximal elements, then no point is isolated
on the right, so that `ğ“•â‚Š i = â¨… j > i, ğ“• j` holds for all `i`. This is in particular the
case when `Î¹ := â„â‰¥0`. -/
lemma rightCont_eq [LinearOrder Î¹] [DenselyOrdered Î¹] [NoMaxOrder Î¹]
    (ğ“• : Filtration Î¹ m) (i : Î¹) :
    ğ“•â‚Š i = â¨… j > i, ğ“• j := ğ“•.rightCont_eq_of_not_isMax (not_isMax i)

variable [PartialOrder Î¹]

lemma le_rightCont (ğ“• : Filtration Î¹ m) : ğ“• â‰¤ ğ“•â‚Š := by
  letI := Preorder.topology Î¹; haveI : OrderTopology Î¹ := âŸ¨rflâŸ©
  intro i
  by_cases hne : (ğ“[>] i).NeBot
  Â· rw [rightCont_eq_of_neBot_nhdsGT]
    exact le_iInfâ‚‚ fun _ he => ğ“•.mono he.le
  Â· rw [rightCont_def, if_neg hne]

lemma rightCont_self (ğ“• : Filtration Î¹ m) : ğ“•â‚Šâ‚Š = ğ“•â‚Š := by
  letI := Preorder.topology Î¹; haveI : OrderTopology Î¹ := âŸ¨rflâŸ©
  apply le_antisymm _ ğ“•â‚Š.le_rightCont
  intro i
  by_cases hne : (ğ“[>] i).NeBot
  Â· have hineq : (â¨… j > i, ğ“•â‚Š j) â‰¤ â¨… j > i, ğ“• j := by
      apply le_iInfâ‚‚ fun u hu => ?_
      have hiou : Set.Ioo i u âˆˆ ğ“[>] i := by
        rw [mem_nhdsWithin_iff_exists_mem_nhds_inter]
        exact âŸ¨Set.Iio u, (isOpen_Iio' u).mem_nhds hu, fun _ hx â†¦ âŸ¨hx.2, hx.1âŸ©âŸ©
      obtain âŸ¨v, hvâŸ© := hne.nonempty_of_mem hiou
      have hleâ‚ : (â¨… j > i, ğ“•â‚Š j) â‰¤ ğ“•â‚Š v := iInfâ‚‚_le_of_le v hv.1 le_rfl
      have hleâ‚‚ : ğ“•â‚Š v â‰¤ ğ“• u := by
        by_cases hnv : (ğ“[>] v).NeBot
        Â· simpa [rightCont_eq_of_neBot_nhdsGT] using iInfâ‚‚_le_of_le u hv.2 le_rfl
        Â· simpa [rightCont_def, hnv] using ğ“•.mono hv.2.le
      exact hleâ‚.trans hleâ‚‚
    simpa [rightCont_eq_of_neBot_nhdsGT] using hineq
  Â· rw [rightCont_def, if_neg hne]

/-- A filtration `ğ“•` is right continuous if it is equal to its right continuation `ğ“•â‚Š`. -/
class IsRightContinuous (ğ“• : Filtration Î¹ m) where
    /-- The right continuity property. -/
    RC : ğ“•â‚Š â‰¤ ğ“•

lemma IsRightContinuous.eq {ğ“• : Filtration Î¹ m} [h : IsRightContinuous ğ“•] :
    ğ“• = ğ“•â‚Š := le_antisymm ğ“•.le_rightCont h.RC

lemma isRightContinuous_rightCont (ğ“• : Filtration Î¹ m) : ğ“•â‚Š.IsRightContinuous :=
  âŸ¨(rightCont_self ğ“•).leâŸ©

lemma IsRightContinuous.measurableSet {ğ“• : Filtration Î¹ m} [IsRightContinuous ğ“•] {i : Î¹}
    {s : Set Î©} (hs : MeasurableSet[ğ“•â‚Š i] s) :
    MeasurableSet[ğ“• i] s := IsRightContinuous.eq (ğ“• := ğ“•) â–¸ hs

/-- A filtration `ğ“•` is said to satisfy the usual conditions if it is right continuous and `ğ“• 0`
  and consequently `ğ“• t` is complete (i.e. contains all null sets) for all `t`. -/
class HasUsualConditions [OrderBot Î¹] (ğ“• : Filtration Î¹ m) (Î¼ : Measure Î© := by volume_tac)
    extends IsRightContinuous ğ“• where
    /-- `ğ“• âŠ¥` contains all the null sets. -/
    IsComplete â¦ƒs : Set Î©â¦„ (hs : Î¼ s = 0) : MeasurableSet[ğ“• âŠ¥] s

variable [OrderBot Î¹]

instance {ğ“• : Filtration Î¹ m} {Î¼ : Measure Î©} [u : HasUsualConditions ğ“• Î¼] {i : Î¹} :
    @Measure.IsComplete Î© (ğ“• i) (Î¼.trim <| ğ“•.le _) :=
  âŸ¨fun _ hs â†¦ ğ“•.mono bot_le _ <| u.2 (measure_eq_zero_of_trim_eq_zero (Filtration.le ğ“• _) hs)âŸ©

lemma HasUsualConditions.measurableSet_of_null
    (ğ“• : Filtration Î¹ m) {Î¼ : Measure Î©} [u : HasUsualConditions ğ“• Î¼] (s : Set Î©) (hs : Î¼ s = 0) :
    MeasurableSet[ğ“• âŠ¥] s :=
  u.2 hs

end IsRightContinuous

variable {Î² : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (Î² i)] [âˆ€ i, MetrizableSpace (Î² i)]
  [mÎ² : âˆ€ i, MeasurableSpace (Î² i)] [âˆ€ i, BorelSpace (Î² i)]
  [Preorder Î¹]

/-- Given a sequence of functions, the natural filtration is the smallest sequence
of Ïƒ-algebras such that the sequence of functions is measurable with respect to
the filtration. -/
def natural (u : (i : Î¹) â†’ Î© â†’ Î² i) (hum : âˆ€ i, StronglyMeasurable (u i)) : Filtration Î¹ m where
  seq i := â¨† j â‰¤ i, MeasurableSpace.comap (u j) (mÎ² j)
  mono' _ _ hij := biSup_mono fun _ => ge_trans hij
  le' i := by
    refine iSupâ‚‚_le ?_
    rintro j _ s âŸ¨t, ht, rflâŸ©
    exact (hum j).measurable ht

section

open MeasurableSpace

theorem filtrationOfSet_eq_natural [âˆ€ i, MulZeroOneClass (Î² i)] [âˆ€ i, Nontrivial (Î² i)]
    {s : Î¹ â†’ Set Î©} (hsm : âˆ€ i, MeasurableSet[m] (s i)) :
    filtrationOfSet hsm = natural (fun i => (s i).indicator (fun _ => 1 : Î© â†’ Î² i)) fun i =>
      stronglyMeasurable_one.indicator (hsm i) := by
  simp only [filtrationOfSet, natural, measurableSpace_iSup_eq, exists_prop, mk.injEq]
  ext1 i
  refine le_antisymm (generateFrom_le ?_) (generateFrom_le ?_)
  Â· rintro _ âŸ¨j, hij, rflâŸ©
    refine measurableSet_generateFrom âŸ¨j, measurableSet_generateFrom âŸ¨hij, ?_âŸ©âŸ©
    rw [comap_eq_generateFrom]
    refine measurableSet_generateFrom âŸ¨{1}, measurableSet_singleton 1, ?_âŸ©
    ext x
    simp
  Â· rintro t âŸ¨n, htâŸ©
    suffices MeasurableSpace.generateFrom {t | n â‰¤ i âˆ§
      MeasurableSet[MeasurableSpace.comap ((s n).indicator (fun _ => 1 : Î© â†’ Î² n)) (mÎ² n)] t} â‰¤
        MeasurableSpace.generateFrom {t | âˆƒ (j : Î¹), j â‰¤ i âˆ§ s j = t} by
      exact this _ ht
    refine generateFrom_le ?_
    rintro t âŸ¨hn, u, _, hu'âŸ©
    obtain heq | heq | heq | heq := Set.indicator_const_preimage (s n) u (1 : Î² n)
    on_goal 4 => rw [Set.mem_singleton_iff] at heq
    all_goals rw [heq] at hu'; rw [â† hu']
    exacts [MeasurableSet.univ, measurableSet_generateFrom âŸ¨n, hn, rflâŸ©,
      MeasurableSet.compl (measurableSet_generateFrom âŸ¨n, hn, rflâŸ©), measurableSet_empty _]

end

section Limit

variable {E : Type*} [Zero E] [TopologicalSpace E] {â„± : Filtration Î¹ m} {f : Î¹ â†’ Î© â†’ E}
  {Î¼ : Measure Î©}

open scoped Classical in
/-- Given a process `f` and a filtration `â„±`, if `f` converges to some `g` almost everywhere and
`g` is `â¨† n, â„± n`-measurable, then `limitProcess f â„± Î¼` chooses said `g`, else it returns 0.

This definition is used to phrase the a.e. martingale convergence theorem
`Submartingale.ae_tendsto_limitProcess` where an LÂ¹-bounded submartingale `f` adapted to `â„±`
converges to `limitProcess f â„± Î¼` `Î¼`-almost everywhere. -/
noncomputable def limitProcess (f : Î¹ â†’ Î© â†’ E) (â„± : Filtration Î¹ m)
    (Î¼ : Measure Î©) :=
  if h : âˆƒ g : Î© â†’ E,
    StronglyMeasurable[â¨† n, â„± n] g âˆ§ âˆ€áµ Ï‰ âˆ‚Î¼, Tendsto (fun n => f n Ï‰) atTop (ğ“ (g Ï‰)) then
  Classical.choose h else 0

theorem stronglyMeasurable_limitProcess : StronglyMeasurable[â¨† n, â„± n] (limitProcess f â„± Î¼) := by
  rw [limitProcess]
  split_ifs with h
  exacts [(Classical.choose_spec h).1, stronglyMeasurable_zero]

theorem stronglyMeasurable_limit_process' : StronglyMeasurable[m] (limitProcess f â„± Î¼) :=
  stronglyMeasurable_limitProcess.mono (sSup_le fun _ âŸ¨_, hnâŸ© => hn â–¸ â„±.le _)

theorem memLp_limitProcess_of_eLpNorm_bdd {R : â„â‰¥0} {p : â„â‰¥0âˆ} {F : Type*} [NormedAddCommGroup F]
    {â„± : Filtration â„• m} {f : â„• â†’ Î© â†’ F} (hfm : âˆ€ n, AEStronglyMeasurable (f n) Î¼)
    (hbdd : âˆ€ n, eLpNorm (f n) p Î¼ â‰¤ R) : MemLp (limitProcess f â„± Î¼) p Î¼ := by
  rw [limitProcess]
  split_ifs with h
  Â· refine âŸ¨StronglyMeasurable.aestronglyMeasurable
      ((Classical.choose_spec h).1.mono (sSup_le fun m âŸ¨n, hnâŸ© => hn â–¸ â„±.le _)),
      lt_of_le_of_lt (Lp.eLpNorm_lim_le_liminf_eLpNorm hfm _ (Classical.choose_spec h).2)
        (lt_of_le_of_lt ?_ (ENNReal.coe_lt_top : â†‘R < âˆ))âŸ©
    simp_rw [liminf_eq, eventually_atTop]
    exact sSup_le fun b âŸ¨a, haâŸ© => (ha a le_rfl).trans (hbdd _)
  Â· exact MemLp.zero

end Limit

section piLE

/-! ### Filtration of the first events -/

open MeasurableSpace Preorder

variable {X : Î¹ â†’ Type*} [âˆ€ i, MeasurableSpace (X i)]

/-- The canonical filtration on the product space `Î  i, X i`, where `piLE i`
consists of measurable sets depending only on coordinates `â‰¤ i`. -/
def piLE : @Filtration (Î  i, X i) Î¹ _ pi where
  seq i := pi.comap (restrictLe i)
  mono' i j hij := by
    simp only
    rw [â† restrictLeâ‚‚_comp_restrictLe hij, â† comap_comp]
    exact comap_mono (measurable_restrictLeâ‚‚ _).comap_le
  le' i := (measurable_restrictLe i).comap_le

variable [LocallyFiniteOrderBot Î¹]

lemma piLE_eq_comap_frestrictLe (i : Î¹) : piLE (X := X) i = pi.comap (frestrictLe i) := by
  apply le_antisymm
  Â· simp_rw [piLE, â† piCongrLeft_comp_frestrictLe, â† MeasurableEquiv.coe_piCongrLeft, â† comap_comp]
    exact MeasurableSpace.comap_mono <| Measurable.comap_le (by fun_prop)
  Â· rw [â† piCongrLeft_comp_restrictLe, â† MeasurableEquiv.coe_piCongrLeft, â† comap_comp]
    exact MeasurableSpace.comap_mono <| Measurable.comap_le (by fun_prop)

end piLE

section piFinset

open MeasurableSpace Finset

variable {Î¹ : Type*} {X : Î¹ â†’ Type*} [âˆ€ i, MeasurableSpace (X i)]

/-- The filtration of events which only depends on finitely many coordinates
on the product space `Î  i, X i`, `piFinset s` consists of measurable sets depending only on
coordinates in `s`, where `s : Finset Î¹`. -/
def piFinset : @Filtration (Î  i, X i) (Finset Î¹) _ pi where
  seq s := pi.comap s.restrict
  mono' s t hst := by
    simp only
    rw [â† restrictâ‚‚_comp_restrict hst, â† comap_comp]
    exact comap_mono (measurable_restrictâ‚‚ hst).comap_le
  le' s := s.measurable_restrict.comap_le

lemma piFinset_eq_comap_restrict (s : Finset Î¹) :
    piFinset (X := X) s = pi.comap (s : Set Î¹).restrict := rfl

end piFinset

variable {Î± : Type*}

/-- The exterior Ïƒ-algebras of finite sets of `Î±` form a cofiltration indexed by `Finset Î±`. -/
def cylinderEventsCompl : Filtration (Finset Î±)áµ’áµˆ (.pi (X := fun _ : Î± â†¦ Î©)) where
  seq Î› := cylinderEvents (â†‘(OrderDual.ofDual Î›))á¶œ
  mono' _ _ h := cylinderEvents_mono <| Set.compl_subset_compl_of_subset h
  le' _  := cylinderEvents_le_pi

end Filtration

end MeasureTheory
