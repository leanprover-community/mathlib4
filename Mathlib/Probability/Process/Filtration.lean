/-
Copyright (c) 2021 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying, RÃ©my Degenne
-/
import Mathlib.MeasureTheory.Function.ConditionalExpectation.Real

#align_import probability.process.filtration from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Filtrations

This file defines filtrations of a measurable space and Ïƒ-finite filtrations.

## Main definitions

* `MeasureTheory.Filtration`: a filtration on a measurable space. That is, a monotone sequence of
  sub-Ïƒ-algebras.
* `MeasureTheory.SigmaFiniteFiltration`: a filtration `f` is Ïƒ-finite with respect to a measure
  `Î¼` if for all `i`, `Î¼.trim (f.le i)` is Ïƒ-finite.
* `MeasureTheory.Filtration.natural`: the smallest filtration that makes a process adapted. That
  notion `adapted` is not defined yet in this file. See `MeasureTheory.adapted`.

## Main results

* `MeasureTheory.Filtration.instCompleteLattice`: filtrations are a complete lattice.

## Tags

filtration, stochastic process

-/


open Filter Order TopologicalSpace

open scoped Classical MeasureTheory NNReal ENNReal Topology

namespace MeasureTheory

/-- A `Filtration` on a measurable space `Î©` with Ïƒ-algebra `m` is a monotone
sequence of sub-Ïƒ-algebras of `m`. -/
structure Filtration {Î© : Type*} (Î¹ : Type*) [Preorder Î¹] (m : MeasurableSpace Î©) where
  seq : Î¹ â†’ MeasurableSpace Î©
  mono' : Monotone seq
  le' : âˆ€ i : Î¹, seq i â‰¤ m
#align measure_theory.filtration MeasureTheory.Filtration

attribute [coe] Filtration.seq

variable {Î© Î² Î¹ : Type*} {m : MeasurableSpace Î©}

instance [Preorder Î¹] : CoeFun (Filtration Î¹ m) fun _ => Î¹ â†’ MeasurableSpace Î© :=
  âŸ¨fun f => f.seqâŸ©

namespace Filtration

variable [Preorder Î¹]

protected theorem mono {i j : Î¹} (f : Filtration Î¹ m) (hij : i â‰¤ j) : f i â‰¤ f j :=
  f.mono' hij
#align measure_theory.filtration.mono MeasureTheory.Filtration.mono

protected theorem le (f : Filtration Î¹ m) (i : Î¹) : f i â‰¤ m :=
  f.le' i
#align measure_theory.filtration.le MeasureTheory.Filtration.le

@[ext]
protected theorem ext {f g : Filtration Î¹ m} (h : (f : Î¹ â†’ MeasurableSpace Î©) = g) : f = g := by
  cases f; cases g; congr
#align measure_theory.filtration.ext MeasureTheory.Filtration.ext

variable (Î¹)

/-- The constant filtration which is equal to `m` for all `i : Î¹`. -/
def const (m' : MeasurableSpace Î©) (hm' : m' â‰¤ m) : Filtration Î¹ m :=
  âŸ¨fun _ => m', monotone_const, fun _ => hm'âŸ©
#align measure_theory.filtration.const MeasureTheory.Filtration.const

variable {Î¹}

@[simp]
theorem const_apply {m' : MeasurableSpace Î©} {hm' : m' â‰¤ m} (i : Î¹) : const Î¹ m' hm' i = m' :=
  rfl
#align measure_theory.filtration.const_apply MeasureTheory.Filtration.const_apply

instance : Inhabited (Filtration Î¹ m) :=
  âŸ¨const Î¹ m le_rflâŸ©

instance : LE (Filtration Î¹ m) :=
  âŸ¨fun f g => âˆ€ i, f i â‰¤ g iâŸ©

instance : Bot (Filtration Î¹ m) :=
  âŸ¨const Î¹ âŠ¥ bot_leâŸ©

instance : Top (Filtration Î¹ m) :=
  âŸ¨const Î¹ m le_rflâŸ©

instance : Sup (Filtration Î¹ m) :=
  âŸ¨fun f g =>
    { seq := fun i => f i âŠ” g i
      mono' := fun _ _ hij =>
        sup_le ((f.mono hij).trans le_sup_left) ((g.mono hij).trans le_sup_right)
      le' := fun i => sup_le (f.le i) (g.le i) }âŸ©

@[norm_cast]
theorem coeFn_sup {f g : Filtration Î¹ m} : â‡‘(f âŠ” g) = â‡‘f âŠ” â‡‘g :=
  rfl
#align measure_theory.filtration.coe_fn_sup MeasureTheory.Filtration.coeFn_sup

instance : Inf (Filtration Î¹ m) :=
  âŸ¨fun f g =>
    { seq := fun i => f i âŠ“ g i
      mono' := fun _ _ hij =>
        le_inf (inf_le_left.trans (f.mono hij)) (inf_le_right.trans (g.mono hij))
      le' := fun i => inf_le_left.trans (f.le i) }âŸ©

@[norm_cast]
theorem coeFn_inf {f g : Filtration Î¹ m} : â‡‘(f âŠ“ g) = â‡‘f âŠ“ â‡‘g :=
  rfl
#align measure_theory.filtration.coe_fn_inf MeasureTheory.Filtration.coeFn_inf

instance : SupSet (Filtration Î¹ m) :=
  âŸ¨fun s =>
    { seq := fun i => sSup ((fun f : Filtration Î¹ m => f i) '' s)
      mono' := fun i j hij => by
        refine sSup_le fun m' hm' => ?_
        rw [Set.mem_image] at hm'
        obtain âŸ¨f, hf_mem, hfm'âŸ© := hm'
        rw [â† hfm']
        refine (f.mono hij).trans ?_
        have hfj_mem : f j âˆˆ (fun g : Filtration Î¹ m => g j) '' s := âŸ¨f, hf_mem, rflâŸ©
        exact le_sSup hfj_mem
      le' := fun i => by
        refine sSup_le fun m' hm' => ?_
        rw [Set.mem_image] at hm'
        obtain âŸ¨f, _, hfm'âŸ© := hm'
        rw [â† hfm']
        exact f.le i }âŸ©

theorem sSup_def (s : Set (Filtration Î¹ m)) (i : Î¹) :
    sSup s i = sSup ((fun f : Filtration Î¹ m => f i) '' s) :=
  rfl
#align measure_theory.filtration.Sup_def MeasureTheory.Filtration.sSup_def

noncomputable instance : InfSet (Filtration Î¹ m) :=
  âŸ¨fun s =>
    { seq := fun i => if Set.Nonempty s then sInf ((fun f : Filtration Î¹ m => f i) '' s) else m
      mono' := fun i j hij => by
        by_cases h_nonempty : Set.Nonempty s
        swap; Â· simp only [h_nonempty, Set.image_nonempty, if_false, le_refl]
        simp only [h_nonempty, if_true, le_sInf_iff, Set.mem_image, forall_exists_index, and_imp,
          forall_apply_eq_imp_iffâ‚‚]
        refine fun f hf_mem => le_trans ?_ (f.mono hij)
        have hfi_mem : f i âˆˆ (fun g : Filtration Î¹ m => g i) '' s := âŸ¨f, hf_mem, rflâŸ©
        exact sInf_le hfi_mem
      le' := fun i => by
        by_cases h_nonempty : Set.Nonempty s
        swap; Â· simp only [h_nonempty, if_false, le_refl]
        simp only [h_nonempty, if_true]
        obtain âŸ¨f, hf_memâŸ© := h_nonempty
        exact le_trans (sInf_le âŸ¨f, hf_mem, rflâŸ©) (f.le i) }âŸ©

theorem sInf_def (s : Set (Filtration Î¹ m)) (i : Î¹) :
    sInf s i = if Set.Nonempty s then sInf ((fun f : Filtration Î¹ m => f i) '' s) else m :=
  rfl
#align measure_theory.filtration.Inf_def MeasureTheory.Filtration.sInf_def

noncomputable instance instCompleteLattice : CompleteLattice (Filtration Î¹ m) where
  le := (Â· â‰¤ Â·)
  le_refl f i := le_rfl
  le_trans f g h h_fg h_gh i := (h_fg i).trans (h_gh i)
  le_antisymm f g h_fg h_gf := Filtration.ext <| funext fun i => (h_fg i).antisymm (h_gf i)
  sup := (Â· âŠ” Â·)
  le_sup_left f g i := le_sup_left
  le_sup_right f g i := le_sup_right
  sup_le f g h h_fh h_gh i := sup_le (h_fh i) (h_gh _)
  inf := (Â· âŠ“ Â·)
  inf_le_left f g i := inf_le_left
  inf_le_right f g i := inf_le_right
  le_inf f g h h_fg h_fh i := le_inf (h_fg i) (h_fh i)
  sSup := sSup
  le_sSup s f hf_mem i := le_sSup âŸ¨f, hf_mem, rflâŸ©
  sSup_le s f h_forall i :=
    sSup_le fun m' hm' => by
      obtain âŸ¨g, hg_mem, hfm'âŸ© := hm'
      rw [â† hfm']
      exact h_forall g hg_mem i
  sInf := sInf
  sInf_le s f hf_mem i := by
    have hs : s.Nonempty := âŸ¨f, hf_memâŸ©
    simp only [sInf_def, hs, if_true]
    exact sInf_le âŸ¨f, hf_mem, rflâŸ©
  le_sInf s f h_forall i := by
    by_cases hs : s.Nonempty
    swap; Â· simp only [sInf_def, hs, if_false]; exact f.le i
    simp only [sInf_def, hs, if_true, le_sInf_iff, Set.mem_image, forall_exists_index, and_imp,
      forall_apply_eq_imp_iffâ‚‚]
    exact fun g hg_mem => h_forall g hg_mem i
  top := âŠ¤
  bot := âŠ¥
  le_top f i := f.le' i
  bot_le f i := bot_le

end Filtration

theorem measurableSet_of_filtration [Preorder Î¹] {f : Filtration Î¹ m} {s : Set Î©} {i : Î¹}
    (hs : MeasurableSet[f i] s) : MeasurableSet[m] s :=
  f.le i s hs
#align measure_theory.measurable_set_of_filtration MeasureTheory.measurableSet_of_filtration

/-- A measure is Ïƒ-finite with respect to filtration if it is Ïƒ-finite with respect
to all the sub-Ïƒ-algebra of the filtration. -/
class SigmaFiniteFiltration [Preorder Î¹] (Î¼ : Measure Î©) (f : Filtration Î¹ m) : Prop where
  SigmaFinite : âˆ€ i : Î¹, SigmaFinite (Î¼.trim (f.le i))
#align measure_theory.sigma_finite_filtration MeasureTheory.SigmaFiniteFiltration

instance sigmaFinite_of_sigmaFiniteFiltration [Preorder Î¹] (Î¼ : Measure Î©) (f : Filtration Î¹ m)
    [hf : SigmaFiniteFiltration Î¼ f] (i : Î¹) : SigmaFinite (Î¼.trim (f.le i)) :=
  hf.SigmaFinite _
#align measure_theory.sigma_finite_of_sigma_finite_filtration MeasureTheory.sigmaFinite_of_sigmaFiniteFiltration

instance (priority := 100) IsFiniteMeasure.sigmaFiniteFiltration [Preorder Î¹] (Î¼ : Measure Î©)
    (f : Filtration Î¹ m) [IsFiniteMeasure Î¼] : SigmaFiniteFiltration Î¼ f :=
  âŸ¨fun n => by infer_instanceâŸ©
#align measure_theory.is_finite_measure.sigma_finite_filtration MeasureTheory.IsFiniteMeasure.sigmaFiniteFiltration

/-- Given an integrable function `g`, the conditional expectations of `g` with respect to a
filtration is uniformly integrable. -/
theorem Integrable.uniformIntegrable_condexp_filtration [Preorder Î¹] {Î¼ : Measure Î©}
    [IsFiniteMeasure Î¼] {f : Filtration Î¹ m} {g : Î© â†’ â„} (hg : Integrable g Î¼) :
    UniformIntegrable (fun i => Î¼[g|f i]) 1 Î¼ :=
  hg.uniformIntegrable_condexp f.le
#align measure_theory.integrable.uniform_integrable_condexp_filtration MeasureTheory.Integrable.uniformIntegrable_condexp_filtration

section OfSet

variable [Preorder Î¹]

/-- Given a sequence of measurable sets `(sâ‚™)`, `filtrationOfSet` is the smallest filtration
such that `sâ‚™` is measurable with respect to the `n`-th sub-Ïƒ-algebra in `filtrationOfSet`. -/
def filtrationOfSet {s : Î¹ â†’ Set Î©} (hsm : âˆ€ i, MeasurableSet (s i)) : Filtration Î¹ m where
  seq i := MeasurableSpace.generateFrom {t | âˆƒ j â‰¤ i, s j = t}
  mono' _ _ hnm := MeasurableSpace.generateFrom_mono fun _ âŸ¨k, hkâ‚, hkâ‚‚âŸ© => âŸ¨k, hkâ‚.trans hnm, hkâ‚‚âŸ©
  le' _ := MeasurableSpace.generateFrom_le fun _ âŸ¨k, _, hkâ‚‚âŸ© => hkâ‚‚ â–¸ hsm k
#align measure_theory.filtration_of_set MeasureTheory.filtrationOfSet

theorem measurableSet_filtrationOfSet {s : Î¹ â†’ Set Î©} (hsm : âˆ€ i, MeasurableSet[m] (s i)) (i : Î¹)
    {j : Î¹} (hj : j â‰¤ i) : MeasurableSet[filtrationOfSet hsm i] (s j) :=
  MeasurableSpace.measurableSet_generateFrom âŸ¨j, hj, rflâŸ©
#align measure_theory.measurable_set_filtration_of_set MeasureTheory.measurableSet_filtrationOfSet

theorem measurableSet_filtrationOfSet' {s : Î¹ â†’ Set Î©} (hsm : âˆ€ n, MeasurableSet[m] (s n))
    (i : Î¹) : MeasurableSet[filtrationOfSet hsm i] (s i) :=
  measurableSet_filtrationOfSet hsm i le_rfl
#align measure_theory.measurable_set_filtration_of_set' MeasureTheory.measurableSet_filtrationOfSet'

end OfSet

namespace Filtration

variable [TopologicalSpace Î²] [MetrizableSpace Î²] [mÎ² : MeasurableSpace Î²] [BorelSpace Î²]
  [Preorder Î¹]

/-- Given a sequence of functions, the natural filtration is the smallest sequence
of Ïƒ-algebras such that that sequence of functions is measurable with respect to
the filtration. -/
def natural (u : Î¹ â†’ Î© â†’ Î²) (hum : âˆ€ i, StronglyMeasurable (u i)) : Filtration Î¹ m where
  seq i := â¨† j â‰¤ i, MeasurableSpace.comap (u j) mÎ²
  mono' i j hij := biSup_mono fun k => ge_trans hij
  le' i := by
    refine iSupâ‚‚_le ?_
    rintro j _ s âŸ¨t, ht, rflâŸ©
    exact (hum j).measurable ht
#align measure_theory.filtration.natural MeasureTheory.Filtration.natural

section

open MeasurableSpace

theorem filtrationOfSet_eq_natural [MulZeroOneClass Î²] [Nontrivial Î²] {s : Î¹ â†’ Set Î©}
    (hsm : âˆ€ i, MeasurableSet[m] (s i)) :
    filtrationOfSet hsm = natural (fun i => (s i).indicator (fun _ => 1 : Î© â†’ Î²)) fun i =>
      stronglyMeasurable_one.indicator (hsm i) := by
  simp only [filtrationOfSet, natural, measurableSpace_iSup_eq, exists_prop, mk.injEq]
  ext1 i
  refine le_antisymm (generateFrom_le ?_) (generateFrom_le ?_)
  Â· rintro _ âŸ¨j, hij, rflâŸ©
    refine measurableSet_generateFrom âŸ¨j, measurableSet_generateFrom âŸ¨hij, ?_âŸ©âŸ©
    rw [comap_eq_generateFrom]
    refine measurableSet_generateFrom âŸ¨{1}, measurableSet_singleton 1, ?_âŸ©
    ext x
    simp [Set.indicator_const_preimage_eq_union]
  Â· rintro t âŸ¨n, htâŸ©
    suffices MeasurableSpace.generateFrom {t | n â‰¤ i âˆ§
      MeasurableSet[MeasurableSpace.comap ((s n).indicator (fun _ => 1 : Î© â†’ Î²)) mÎ²] t} â‰¤
        MeasurableSpace.generateFrom {t | âˆƒ (j : Î¹), j â‰¤ i âˆ§ s j = t} by
      exact this _ ht
    refine generateFrom_le ?_
    rintro t âŸ¨hn, u, _, hu'âŸ©
    obtain heq | heq | heq | heq := Set.indicator_const_preimage (s n) u (1 : Î²)
    on_goal 4 => rw [Set.mem_singleton_iff] at heq
    all_goals rw [heq] at hu'; rw [â† hu']
    exacts [MeasurableSet.univ, measurableSet_generateFrom âŸ¨n, hn, rflâŸ©,
      MeasurableSet.compl (measurableSet_generateFrom âŸ¨n, hn, rflâŸ©), measurableSet_empty _]
#align measure_theory.filtration.filtration_of_set_eq_natural MeasureTheory.Filtration.filtrationOfSet_eq_natural

end

section Limit

variable {E : Type*} [Zero E] [TopologicalSpace E] {â„± : Filtration Î¹ m} {f : Î¹ â†’ Î© â†’ E}
  {Î¼ : Measure Î©}

/-- Given a process `f` and a filtration `â„±`, if `f` converges to some `g` almost everywhere and
`g` is `â¨† n, â„± n`-measurable, then `limitProcess f â„± Î¼` chooses said `g`, else it returns 0.

This definition is used to phrase the a.e. martingale convergence theorem
`Submartingale.ae_tendsto_limitProcess` where an LÂ¹-bounded submartingale `f` adapted to `â„±`
converges to `limitProcess f â„± Î¼` `Î¼`-almost everywhere. -/
noncomputable def limitProcess (f : Î¹ â†’ Î© â†’ E) (â„± : Filtration Î¹ m)
    (Î¼ : Measure Î©) :=
  if h : âˆƒ g : Î© â†’ E,
    StronglyMeasurable[â¨† n, â„± n] g âˆ§ âˆ€áµ Ï‰ âˆ‚Î¼, Tendsto (fun n => f n Ï‰) atTop (ð“ (g Ï‰)) then
  Classical.choose h else 0
#align measure_theory.filtration.limit_process MeasureTheory.Filtration.limitProcess

theorem stronglyMeasurable_limitProcess : StronglyMeasurable[â¨† n, â„± n] (limitProcess f â„± Î¼) := by
  rw [limitProcess]
  split_ifs with h
  exacts [(Classical.choose_spec h).1, stronglyMeasurable_zero]
#align measure_theory.filtration.strongly_measurable_limit_process MeasureTheory.Filtration.stronglyMeasurable_limitProcess

theorem stronglyMeasurable_limit_process' : StronglyMeasurable[m] (limitProcess f â„± Î¼) :=
  stronglyMeasurable_limitProcess.mono (sSup_le fun _ âŸ¨_, hnâŸ© => hn â–¸ â„±.le _)
#align measure_theory.filtration.strongly_measurable_limit_process' MeasureTheory.Filtration.stronglyMeasurable_limit_process'

theorem memâ„’p_limitProcess_of_snorm_bdd {R : â„â‰¥0} {p : â„â‰¥0âˆž} {F : Type*} [NormedAddCommGroup F]
    {â„± : Filtration â„• m} {f : â„• â†’ Î© â†’ F} (hfm : âˆ€ n, AEStronglyMeasurable (f n) Î¼)
    (hbdd : âˆ€ n, snorm (f n) p Î¼ â‰¤ R) : Memâ„’p (limitProcess f â„± Î¼) p Î¼ := by
  rw [limitProcess]
  split_ifs with h
  Â· refine âŸ¨StronglyMeasurable.aestronglyMeasurable
      ((Classical.choose_spec h).1.mono (sSup_le fun m âŸ¨n, hnâŸ© => hn â–¸ â„±.le _)),
      lt_of_le_of_lt (Lp.snorm_lim_le_liminf_snorm hfm _ (Classical.choose_spec h).2)
        (lt_of_le_of_lt ?_ (ENNReal.coe_lt_top : â†‘R < âˆž))âŸ©
    simp_rw [liminf_eq, eventually_atTop]
    exact sSup_le fun b âŸ¨a, haâŸ© => (ha a le_rfl).trans (hbdd _)
  Â· exact zero_memâ„’p
#align measure_theory.filtration.mem_â„’p_limit_process_of_snorm_bdd MeasureTheory.Filtration.memâ„’p_limitProcess_of_snorm_bdd

end Limit

end Filtration

end MeasureTheory
