/-
Copyright (c) 2021 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying, RÃ©my Degenne
-/
import Mathlib.Probability.Process.Filtration
import Mathlib.Topology.Instances.Discrete

#align_import probability.process.adapted from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Adapted and progressively measurable processes

This file defines some standard definition from the theory of stochastic processes including
filtrations and stopping times. These definitions are used to model the amount of information
at a specific time and are the first step in formalizing stochastic processes.

## Main definitions

* `MeasureTheory.Adapted`: a sequence of functions `u` is said to be adapted to a
  filtration `f` if at each point in time `i`, `u i` is `f i`-strongly measurable
* `MeasureTheory.ProgMeasurable`: a sequence of functions `u` is said to be progressively
  measurable with respect to a filtration `f` if at each point in time `i`, `u` restricted to
  `Set.Iic i Ã— Î©` is strongly measurable with respect to the product `MeasurableSpace` structure
  where the Ïƒ-algebra used for `Î©` is `f i`.

## Main results

* `Adapted.progMeasurable_of_continuous`: a continuous adapted process is progressively measurable.

## Tags

adapted, progressively measurable

-/


open Filter Order TopologicalSpace

open scoped Classical MeasureTheory NNReal ENNReal Topology

namespace MeasureTheory

variable {Î© Î² Î¹ : Type*} {m : MeasurableSpace Î©} [TopologicalSpace Î²] [Preorder Î¹]
  {u v : Î¹ â†’ Î© â†’ Î²} {f : Filtration Î¹ m}

/-- A sequence of functions `u` is adapted to a filtration `f` if for all `i`,
`u i` is `f i`-measurable. -/
def Adapted (f : Filtration Î¹ m) (u : Î¹ â†’ Î© â†’ Î²) : Prop :=
  âˆ€ i : Î¹, StronglyMeasurable[f i] (u i)
#align measure_theory.adapted MeasureTheory.Adapted

namespace Adapted

@[to_additive]
protected theorem mul [Mul Î²] [ContinuousMul Î²] (hu : Adapted f u) (hv : Adapted f v) :
    Adapted f (u * v) := fun i => (hu i).mul (hv i)
#align measure_theory.adapted.mul MeasureTheory.Adapted.mul
#align measure_theory.adapted.add MeasureTheory.Adapted.add

@[to_additive]
protected theorem div [Div Î²] [ContinuousDiv Î²] (hu : Adapted f u) (hv : Adapted f v) :
    Adapted f (u / v) := fun i => (hu i).div (hv i)
#align measure_theory.adapted.div MeasureTheory.Adapted.div
#align measure_theory.adapted.sub MeasureTheory.Adapted.sub

@[to_additive]
protected theorem inv [Group Î²] [TopologicalGroup Î²] (hu : Adapted f u) :
    Adapted f uâ»Â¹ := fun i => (hu i).inv
#align measure_theory.adapted.inv MeasureTheory.Adapted.inv
#align measure_theory.adapted.neg MeasureTheory.Adapted.neg

protected theorem smul [SMul â„ Î²] [ContinuousSMul â„ Î²] (c : â„) (hu : Adapted f u) :
    Adapted f (c â€¢ u) := fun i => (hu i).const_smul c
#align measure_theory.adapted.smul MeasureTheory.Adapted.smul

protected theorem stronglyMeasurable {i : Î¹} (hf : Adapted f u) : StronglyMeasurable[m] (u i) :=
  (hf i).mono (f.le i)
#align measure_theory.adapted.strongly_measurable MeasureTheory.Adapted.stronglyMeasurable

theorem stronglyMeasurable_le {i j : Î¹} (hf : Adapted f u) (hij : i â‰¤ j) :
    StronglyMeasurable[f j] (u i) := (hf i).mono (f.mono hij)
#align measure_theory.adapted.strongly_measurable_le MeasureTheory.Adapted.stronglyMeasurable_le

end Adapted

theorem adapted_const (f : Filtration Î¹ m) (x : Î²) : Adapted f fun _ _ => x := fun _ =>
  stronglyMeasurable_const
#align measure_theory.adapted_const MeasureTheory.adapted_const

variable (Î²)

theorem adapted_zero [Zero Î²] (f : Filtration Î¹ m) : Adapted f (0 : Î¹ â†’ Î© â†’ Î²) := fun i =>
  @stronglyMeasurable_zero Î© Î² (f i) _ _
#align measure_theory.adapted_zero MeasureTheory.adapted_zero

variable {Î²}

theorem Filtration.adapted_natural [MetrizableSpace Î²] [mÎ² : MeasurableSpace Î²] [BorelSpace Î²]
    {u : Î¹ â†’ Î© â†’ Î²} (hum : âˆ€ i, StronglyMeasurable[m] (u i)) :
    Adapted (Filtration.natural u hum) u := by
  intro i
  refine StronglyMeasurable.mono ?_ (le_iSupâ‚‚_of_le i (le_refl i) le_rfl)
  rw [stronglyMeasurable_iff_measurable_separable]
  exact âŸ¨measurable_iff_comap_le.2 le_rfl, (hum i).isSeparable_rangeâŸ©
#align measure_theory.filtration.adapted_natural MeasureTheory.Filtration.adapted_natural

/-- Progressively measurable process. A sequence of functions `u` is said to be progressively
measurable with respect to a filtration `f` if at each point in time `i`, `u` restricted to
`Set.Iic i Ã— Î©` is measurable with respect to the product `MeasurableSpace` structure where the
Ïƒ-algebra used for `Î©` is `f i`.
The usual definition uses the interval `[0,i]`, which we replace by `Set.Iic i`. We recover the
usual definition for index types `â„â‰¥0` or `â„•`. -/
def ProgMeasurable [MeasurableSpace Î¹] (f : Filtration Î¹ m) (u : Î¹ â†’ Î© â†’ Î²) : Prop :=
  âˆ€ i, StronglyMeasurable[Subtype.instMeasurableSpace.prod (f i)] fun p : Set.Iic i Ã— Î© => u p.1 p.2
#align measure_theory.prog_measurable MeasureTheory.ProgMeasurable

theorem progMeasurable_const [MeasurableSpace Î¹] (f : Filtration Î¹ m) (b : Î²) :
    ProgMeasurable f (fun _ _ => b : Î¹ â†’ Î© â†’ Î²) := fun i =>
  @stronglyMeasurable_const _ _ (Subtype.instMeasurableSpace.prod (f i)) _ _
#align measure_theory.prog_measurable_const MeasureTheory.progMeasurable_const

namespace ProgMeasurable

variable [MeasurableSpace Î¹]

protected theorem adapted (h : ProgMeasurable f u) : Adapted f u := by
  intro i
  have : u i = (fun p : Set.Iic i Ã— Î© => u p.1 p.2) âˆ˜ fun x => (âŸ¨i, Set.mem_Iic.mpr le_rflâŸ©, x) :=
    rfl
  rw [this]
  exact (h i).comp_measurable measurable_prod_mk_left
#align measure_theory.prog_measurable.adapted MeasureTheory.ProgMeasurable.adapted

protected theorem comp {t : Î¹ â†’ Î© â†’ Î¹} [TopologicalSpace Î¹] [BorelSpace Î¹] [MetrizableSpace Î¹]
    (h : ProgMeasurable f u) (ht : ProgMeasurable f t) (ht_le : âˆ€ i Ï‰, t i Ï‰ â‰¤ i) :
    ProgMeasurable f fun i Ï‰ => u (t i Ï‰) Ï‰ := by
  intro i
  have : (fun p : â†¥(Set.Iic i) Ã— Î© => u (t (p.fst : Î¹) p.snd) p.snd) =
    (fun p : â†¥(Set.Iic i) Ã— Î© => u (p.fst : Î¹) p.snd) âˆ˜ fun p : â†¥(Set.Iic i) Ã— Î© =>
      (âŸ¨t (p.fst : Î¹) p.snd, Set.mem_Iic.mpr ((ht_le _ _).trans p.fst.prop)âŸ©, p.snd) := rfl
  rw [this]
  exact (h i).comp_measurable ((ht i).measurable.subtype_mk.prod_mk measurable_snd)
#align measure_theory.prog_measurable.comp MeasureTheory.ProgMeasurable.comp

section Arithmetic

@[to_additive]
protected theorem mul [Mul Î²] [ContinuousMul Î²] (hu : ProgMeasurable f u)
    (hv : ProgMeasurable f v) : ProgMeasurable f fun i Ï‰ => u i Ï‰ * v i Ï‰ := fun i =>
  (hu i).mul (hv i)
#align measure_theory.prog_measurable.mul MeasureTheory.ProgMeasurable.mul
#align measure_theory.prog_measurable.add MeasureTheory.ProgMeasurable.add

@[to_additive]
protected theorem finset_prod' {Î³} [CommMonoid Î²] [ContinuousMul Î²] {U : Î³ â†’ Î¹ â†’ Î© â†’ Î²}
    {s : Finset Î³} (h : âˆ€ c âˆˆ s, ProgMeasurable f (U c)) : ProgMeasurable f (âˆ c âˆˆ s, U c) :=
  Finset.prod_induction U (ProgMeasurable f) (fun _ _ => ProgMeasurable.mul)
    (progMeasurable_const _ 1) h
#align measure_theory.prog_measurable.finset_prod' MeasureTheory.ProgMeasurable.finset_prod'
#align measure_theory.prog_measurable.finset_sum' MeasureTheory.ProgMeasurable.finset_sum'

@[to_additive]
protected theorem finset_prod {Î³} [CommMonoid Î²] [ContinuousMul Î²] {U : Î³ â†’ Î¹ â†’ Î© â†’ Î²}
    {s : Finset Î³} (h : âˆ€ c âˆˆ s, ProgMeasurable f (U c)) :
    ProgMeasurable f fun i a => âˆ c âˆˆ s, U c i a := by
  convert ProgMeasurable.finset_prod' h using 1; ext (i a); simp only [Finset.prod_apply]
#align measure_theory.prog_measurable.finset_prod MeasureTheory.ProgMeasurable.finset_prod
#align measure_theory.prog_measurable.finset_sum MeasureTheory.ProgMeasurable.finset_sum

@[to_additive]
protected theorem inv [Group Î²] [TopologicalGroup Î²] (hu : ProgMeasurable f u) :
    ProgMeasurable f fun i Ï‰ => (u i Ï‰)â»Â¹ := fun i => (hu i).inv
#align measure_theory.prog_measurable.inv MeasureTheory.ProgMeasurable.inv
#align measure_theory.prog_measurable.neg MeasureTheory.ProgMeasurable.neg

@[to_additive]
protected theorem div [Group Î²] [TopologicalGroup Î²] (hu : ProgMeasurable f u)
    (hv : ProgMeasurable f v) : ProgMeasurable f fun i Ï‰ => u i Ï‰ / v i Ï‰ := fun i =>
  (hu i).div (hv i)
#align measure_theory.prog_measurable.div MeasureTheory.ProgMeasurable.div
#align measure_theory.prog_measurable.sub MeasureTheory.ProgMeasurable.sub

end Arithmetic

end ProgMeasurable

theorem progMeasurable_of_tendsto' {Î³} [MeasurableSpace Î¹] [PseudoMetrizableSpace Î²]
    (fltr : Filter Î³) [fltr.NeBot] [fltr.IsCountablyGenerated] {U : Î³ â†’ Î¹ â†’ Î© â†’ Î²}
    (h : âˆ€ l, ProgMeasurable f (U l)) (h_tendsto : Tendsto U fltr (ð“ u)) : ProgMeasurable f u := by
  intro i
  apply @stronglyMeasurable_of_tendsto (Set.Iic i Ã— Î©) Î² Î³
    (MeasurableSpace.prod _ (f i)) _ _ fltr _ _ _ _ fun l => h l i
  rw [tendsto_pi_nhds] at h_tendsto âŠ¢
  intro x
  specialize h_tendsto x.fst
  rw [tendsto_nhds] at h_tendsto âŠ¢
  exact fun s hs h_mem => h_tendsto {g | g x.snd âˆˆ s} (hs.preimage (continuous_apply x.snd)) h_mem
#align measure_theory.prog_measurable_of_tendsto' MeasureTheory.progMeasurable_of_tendsto'

theorem progMeasurable_of_tendsto [MeasurableSpace Î¹] [PseudoMetrizableSpace Î²] {U : â„• â†’ Î¹ â†’ Î© â†’ Î²}
    (h : âˆ€ l, ProgMeasurable f (U l)) (h_tendsto : Tendsto U atTop (ð“ u)) : ProgMeasurable f u :=
  progMeasurable_of_tendsto' atTop h h_tendsto
#align measure_theory.prog_measurable_of_tendsto MeasureTheory.progMeasurable_of_tendsto

/-- A continuous and adapted process is progressively measurable. -/
theorem Adapted.progMeasurable_of_continuous [TopologicalSpace Î¹] [MetrizableSpace Î¹]
    [SecondCountableTopology Î¹] [MeasurableSpace Î¹] [OpensMeasurableSpace Î¹]
    [PseudoMetrizableSpace Î²] (h : Adapted f u) (hu_cont : âˆ€ Ï‰, Continuous fun i => u i Ï‰) :
    ProgMeasurable f u := fun i =>
  @stronglyMeasurable_uncurry_of_continuous_of_stronglyMeasurable _ _ (Set.Iic i) _ _ _ _ _ _ _
    (f i) _ (fun Ï‰ => (hu_cont Ï‰).comp continuous_induced_dom) fun j => (h j).mono (f.mono j.prop)
#align measure_theory.adapted.prog_measurable_of_continuous MeasureTheory.Adapted.progMeasurable_of_continuous

/-- For filtrations indexed by a discrete order, `Adapted` and `ProgMeasurable` are equivalent.
This lemma provides `Adapted f u â†’ ProgMeasurable f u`.
See `ProgMeasurable.adapted` for the reverse direction, which is true more generally. -/
theorem Adapted.progMeasurable_of_discrete [TopologicalSpace Î¹] [DiscreteTopology Î¹]
    [SecondCountableTopology Î¹] [MeasurableSpace Î¹] [OpensMeasurableSpace Î¹]
    [PseudoMetrizableSpace Î²] (h : Adapted f u) : ProgMeasurable f u :=
  h.progMeasurable_of_continuous fun _ => continuous_of_discreteTopology
#align measure_theory.adapted.prog_measurable_of_discrete MeasureTheory.Adapted.progMeasurable_of_discrete

-- this dot notation will make more sense once we have a more general definition for predictable
theorem Predictable.adapted {f : Filtration â„• m} {u : â„• â†’ Î© â†’ Î²} (hu : Adapted f fun n => u (n + 1))
    (hu0 : StronglyMeasurable[f 0] (u 0)) : Adapted f u := fun n =>
  match n with
  | 0 => hu0
  | n + 1 => (hu n).mono (f.mono n.le_succ)
#align measure_theory.predictable.adapted MeasureTheory.Predictable.adapted

end MeasureTheory
