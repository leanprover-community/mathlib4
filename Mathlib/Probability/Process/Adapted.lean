/-
Copyright (c) 2021 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying, RÃ©my Degenne
-/
module

public import Mathlib.Probability.Process.Filtration
public import Mathlib.Topology.Instances.Discrete

/-!
# Adapted and progressively measurable processes

This file defines the related notions of a process `u` being `Adapted`, `StronglyAdapted`
or `ProgMeasurable` (progressively measurable) with respect to a filter `f`, and proves
some basic facts about them.

## Main definitions

* `MeasureTheory.Adapted`: a sequence of functions `u` is said to be adapted to a
  filtration `f` if at each point in time `i`, `u i` is `f i`-measurable
* `MeasureTheory.StronglyAdapted`: a sequence of functions `u` is said to be strongly adapted to a
  filtration `f` if at each point in time `i`, `u i` is `f i`-strongly measurable
* `MeasureTheory.ProgMeasurable`: a sequence of functions `u` is said to be progressively
  measurable with respect to a filtration `f` if at each point in time `i`, `u` restricted to
  `Set.Iic i Ã— Î©` is strongly measurable with respect to the product `MeasurableSpace` structure
  where the Ïƒ-algebra used for `Î©` is `f i`.

## Main results

* `StronglyAdapted.progMeasurable_of_continuous`: a continuous strongly adapted process is
  progressively measurable.

## Tags

adapted, progressively measurable

-/

@[expose] public section

open Filter Order TopologicalSpace

open scoped MeasureTheory NNReal ENNReal Topology

namespace MeasureTheory

variable {Î© Î¹ : Type*} {m : MeasurableSpace Î©} [Preorder Î¹] {f : Filtration Î¹ m}

section Adapted

variable {Î² : Î¹ â†’ Type*} [âˆ€ i, MeasurableSpace (Î² i)] {u v : (i : Î¹) â†’ Î© â†’ Î² i}

/-- A sequence of functions `u` is adapted to a filtration `f` if for all `i`,
`u i` is `f i`-measurable. -/
def Adapted (f : Filtration Î¹ m) (u : (i : Î¹) â†’ Î© â†’ Î² i) : Prop :=
  âˆ€ i : Î¹, Measurable[f i] (u i)

namespace Adapted

@[to_additive]
protected theorem mul [âˆ€ i, Mul (Î² i)] [âˆ€ i, MeasurableMulâ‚‚ (Î² i)]
    (hu : Adapted f u) (hv : Adapted f v) :
    Adapted f (u * v) := fun i => (hu i).mul (hv i)

@[to_additive]
protected theorem div [âˆ€ i, Div (Î² i)] [âˆ€ i, MeasurableDivâ‚‚ (Î² i)]
    (hu : Adapted f u) (hv : Adapted f v) :
    Adapted f (u / v) := fun i => (hu i).div (hv i)

@[to_additive]
protected theorem inv [âˆ€ i, Group (Î² i)] [âˆ€ i, MeasurableInv (Î² i)] (hu : Adapted f u) :
    Adapted f uâ»Â¹ := fun i => (hu i).inv

protected theorem smul {ð•‚ : Type*} [MeasurableSpace ð•‚]
    [âˆ€ i, SMul ð•‚ (Î² i)] [âˆ€ i, MeasurableSMul ð•‚ (Î² i)] (c : ð•‚) (hu : Adapted f u) :
    Adapted f (c â€¢ u) := fun i => (hu i).const_smul c

protected theorem measurable {i : Î¹} (hf : Adapted f u) : Measurable[m] (u i) :=
  (hf i).mono (f.le i) (by rfl)

theorem measurable_le {i j : Î¹} (hf : Adapted f u) (hij : i â‰¤ j) : Measurable[f j] (u i) :=
  (hf i).mono (f.mono hij) (by rfl)

end Adapted

theorem adapted_const' (f : Filtration Î¹ m) (x : (i : Î¹) â†’ Î² i) : Adapted f fun i _ â†¦ x i :=
  fun _ â†¦ measurable_const

theorem adapted_const {Î² : Type*} [MeasurableSpace Î²] (f : Filtration Î¹ m) (x : Î²) :
    Adapted f fun _ _ â†¦ x := adapted_const' _ _

end Adapted

section StronglyAdapted

variable {Î² : Î¹ â†’ Type*} [âˆ€ i, TopologicalSpace (Î² i)] {u v : (i : Î¹) â†’ Î© â†’ Î² i}

/-- A sequence of functions `u` is strongly adapted to a filtration `f` if for all `i`,
`u i` is `f i`-strongly measurable. -/
def StronglyAdapted (f : Filtration Î¹ m) (u : (i : Î¹) â†’ Î© â†’ Î² i) : Prop :=
  âˆ€ i : Î¹, StronglyMeasurable[f i] (u i)

namespace StronglyAdapted

@[to_additive]
protected theorem mul [âˆ€ i, Mul (Î² i)] [âˆ€ i, ContinuousMul (Î² i)]
    (hu : StronglyAdapted f u) (hv : StronglyAdapted f v) :
    StronglyAdapted f (u * v) := fun i => (hu i).mul (hv i)

@[to_additive]
protected theorem div [âˆ€ i, Div (Î² i)] [âˆ€ i, ContinuousDiv (Î² i)]
    (hu : StronglyAdapted f u) (hv : StronglyAdapted f v) :
    StronglyAdapted f (u / v) := fun i => (hu i).div (hv i)

@[to_additive]
protected theorem inv [âˆ€ i, Group (Î² i)] [âˆ€ i, ContinuousInv (Î² i)] (hu : StronglyAdapted f u) :
    StronglyAdapted f uâ»Â¹ := fun i => (hu i).inv

protected theorem smul [âˆ€ i, SMul â„ (Î² i)] [âˆ€ i, ContinuousConstSMul â„ (Î² i)]
    (c : â„) (hu : StronglyAdapted f u) :
    StronglyAdapted f (c â€¢ u) := fun i => (hu i).const_smul c

protected theorem stronglyMeasurable {i : Î¹} (hf : StronglyAdapted f u) :
    StronglyMeasurable[m] (u i) := (hf i).mono (f.le i)

theorem stronglyMeasurable_le {i j : Î¹} (hf : StronglyAdapted f u) (hij : i â‰¤ j) :
    StronglyMeasurable[f j] (u i) := (hf i).mono (f.mono hij)

end StronglyAdapted

theorem StronglyAdapted.adapted [mÎ’ : âˆ€ i, MeasurableSpace (Î² i)] [âˆ€ i, BorelSpace (Î² i)]
    [âˆ€ i, PseudoMetrizableSpace (Î² i)] (hf : StronglyAdapted f u) :
    Adapted f u := fun _ â†¦ (hf _).measurable

theorem Adapted.stronglyAdapted [mÎ’ : âˆ€ i, MeasurableSpace (Î² i)]
    [âˆ€ i, OpensMeasurableSpace (Î² i)] [âˆ€ i, PseudoMetrizableSpace (Î² i)]
    [âˆ€ i, SecondCountableTopology (Î² i)] (hf : Adapted f u) :
    StronglyAdapted f u := fun _ â†¦ (hf _).stronglyMeasurable

theorem stronglyAdapted_iff_adapted [mÎ’ : âˆ€ i, MeasurableSpace (Î² i)]
    [âˆ€ i, BorelSpace (Î² i)] [âˆ€ i, PseudoMetrizableSpace (Î² i)]
    [âˆ€ i, SecondCountableTopology (Î² i)] :
    StronglyAdapted f u â†” Adapted f u := âŸ¨fun h â†¦ h.adapted, fun h â†¦ h.stronglyAdaptedâŸ©

theorem stronglyAdapted_const' (f : Filtration Î¹ m) (x : (i : Î¹) â†’ Î² i) :
    StronglyAdapted f fun i _ â†¦ x i :=
  fun _ â†¦ stronglyMeasurable_const

theorem stronglyAdapted_const {Î² : Type*} [TopologicalSpace Î²] (f : Filtration Î¹ m) (x : Î²) :
    StronglyAdapted f fun _ _ â†¦ x :=
  stronglyAdapted_const' _ _

variable (Î²) in
theorem stronglyAdapted_zero' [âˆ€ i, Zero (Î² i)] (f : Filtration Î¹ m) :
    StronglyAdapted f (0 : (i : Î¹) â†’ Î© â†’ Î² i) :=
  fun i â†¦ @stronglyMeasurable_zero Î© (Î² i) (f i) _ _

theorem stronglyAdapted_zero (Î² : Type*) [TopologicalSpace Î²] [Zero Î²] (f : Filtration Î¹ m) :
    StronglyAdapted f (0 : Î¹ â†’ Î© â†’ Î²) :=
  fun i â†¦ @stronglyMeasurable_zero Î© Î² (f i) _ _

theorem Filtration.stronglyAdapted_natural [âˆ€ i, MetrizableSpace (Î² i)]
    [mÎ² : âˆ€ i, MeasurableSpace (Î² i)] [âˆ€ i, BorelSpace (Î² i)]
    (hum : âˆ€ i, StronglyMeasurable[m] (u i)) :
    StronglyAdapted (Filtration.natural u hum) u := by
  intro i
  refine StronglyMeasurable.mono ?_ (le_iSupâ‚‚_of_le i (le_refl i) le_rfl)
  rw [stronglyMeasurable_iff_measurable_separable]
  exact âŸ¨measurable_iff_comap_le.2 le_rfl, (hum i).isSeparable_rangeâŸ©

end StronglyAdapted

variable {Î² : Type*} [TopologicalSpace Î²] {u v : Î¹ â†’ Î© â†’ Î²}

/-- Progressively measurable process. A sequence of functions `u` is said to be progressively
measurable with respect to a filtration `f` if at each point in time `i`, `u` restricted to
`Set.Iic i Ã— Î©` is measurable with respect to the product `MeasurableSpace` structure where the
Ïƒ-algebra used for `Î©` is `f i`.
The usual definition uses the interval `[0,i]`, which we replace by `Set.Iic i`. We recover the
usual definition for index types `â„â‰¥0` or `â„•`. -/
def ProgMeasurable [MeasurableSpace Î¹] (f : Filtration Î¹ m) (u : Î¹ â†’ Î© â†’ Î²) : Prop :=
  âˆ€ i, StronglyMeasurable[Subtype.instMeasurableSpace.prod (f i)] fun p : Set.Iic i Ã— Î© => u p.1 p.2

theorem progMeasurable_const [MeasurableSpace Î¹] (f : Filtration Î¹ m) (b : Î²) :
    ProgMeasurable f (fun _ _ => b : Î¹ â†’ Î© â†’ Î²) := fun i =>
  @stronglyMeasurable_const _ _ (Subtype.instMeasurableSpace.prod (f i)) _ _

namespace ProgMeasurable

variable [MeasurableSpace Î¹]

protected theorem stronglyAdapted (h : ProgMeasurable f u) : StronglyAdapted f u := by
  intro i
  have : u i = (fun p : Set.Iic i Ã— Î© => u p.1 p.2) âˆ˜ fun x => (âŸ¨i, Set.mem_Iic.mpr le_rflâŸ©, x) :=
    rfl
  rw [this]
  exact (h i).comp_measurable measurable_prodMk_left

protected theorem comp {t : Î¹ â†’ Î© â†’ Î¹} [TopologicalSpace Î¹] [BorelSpace Î¹] [PseudoMetrizableSpace Î¹]
    (h : ProgMeasurable f u) (ht : ProgMeasurable f t) (ht_le : âˆ€ i Ï‰, t i Ï‰ â‰¤ i) :
    ProgMeasurable f fun i Ï‰ => u (t i Ï‰) Ï‰ := by
  intro i
  have : (fun p : â†¥(Set.Iic i) Ã— Î© => u (t (p.fst : Î¹) p.snd) p.snd) =
    (fun p : â†¥(Set.Iic i) Ã— Î© => u (p.fst : Î¹) p.snd) âˆ˜ fun p : â†¥(Set.Iic i) Ã— Î© =>
      (âŸ¨t (p.fst : Î¹) p.snd, Set.mem_Iic.mpr ((ht_le _ _).trans p.fst.prop)âŸ©, p.snd) := rfl
  rw [this]
  exact (h i).comp_measurable ((ht i).measurable.subtype_mk.prodMk measurable_snd)

section Arithmetic

@[to_additive]
protected theorem mul [Mul Î²] [ContinuousMul Î²] (hu : ProgMeasurable f u)
    (hv : ProgMeasurable f v) : ProgMeasurable f fun i Ï‰ => u i Ï‰ * v i Ï‰ := fun i =>
  (hu i).mul (hv i)

@[to_additive]
protected theorem finset_prod' {Î³} [CommMonoid Î²] [ContinuousMul Î²] {U : Î³ â†’ Î¹ â†’ Î© â†’ Î²}
    {s : Finset Î³} (h : âˆ€ c âˆˆ s, ProgMeasurable f (U c)) : ProgMeasurable f (âˆ c âˆˆ s, U c) :=
  Finset.prod_induction U (ProgMeasurable f) (fun _ _ => ProgMeasurable.mul)
    (progMeasurable_const _ 1) h

@[to_additive]
protected theorem finset_prod {Î³} [CommMonoid Î²] [ContinuousMul Î²] {U : Î³ â†’ Î¹ â†’ Î© â†’ Î²}
    {s : Finset Î³} (h : âˆ€ c âˆˆ s, ProgMeasurable f (U c)) :
    ProgMeasurable f fun i a => âˆ c âˆˆ s, U c i a := by
  convert ProgMeasurable.finset_prod' h using 1; ext (i a); simp only [Finset.prod_apply]

@[to_additive]
protected theorem inv [Group Î²] [ContinuousInv Î²] (hu : ProgMeasurable f u) :
    ProgMeasurable f fun i Ï‰ => (u i Ï‰)â»Â¹ := fun i => (hu i).inv

@[to_additive]
protected theorem div [Group Î²] [ContinuousDiv Î²] (hu : ProgMeasurable f u)
    (hv : ProgMeasurable f v) : ProgMeasurable f fun i Ï‰ => u i Ï‰ / v i Ï‰ := fun i =>
  (hu i).div (hv i)

end Arithmetic

end ProgMeasurable

theorem progMeasurable_of_tendsto' {Î³} [MeasurableSpace Î¹] [PseudoMetrizableSpace Î²]
    (fltr : Filter Î³) [fltr.NeBot] [fltr.IsCountablyGenerated] {U : Î³ â†’ Î¹ â†’ Î© â†’ Î²}
    (h : âˆ€ l, ProgMeasurable f (U l)) (h_tendsto : Tendsto U fltr (ð“ u)) : ProgMeasurable f u := by
  intro i
  apply @stronglyMeasurable_of_tendsto (Set.Iic i Ã— Î©) Î² Î³
    (MeasurableSpace.prod _ (f i)) _ _ fltr _ _ _ _ fun l => h l i
  rw [tendsto_pi_nhds] at h_tendsto âŠ¢
  intro x
  specialize h_tendsto x.fst
  rw [tendsto_nhds] at h_tendsto âŠ¢
  exact fun s hs h_mem => h_tendsto {g | g x.snd âˆˆ s} (hs.preimage (continuous_apply x.snd)) h_mem

theorem progMeasurable_of_tendsto [MeasurableSpace Î¹] [PseudoMetrizableSpace Î²] {U : â„• â†’ Î¹ â†’ Î© â†’ Î²}
    (h : âˆ€ l, ProgMeasurable f (U l)) (h_tendsto : Tendsto U atTop (ð“ u)) : ProgMeasurable f u :=
  progMeasurable_of_tendsto' atTop h h_tendsto

/-- A continuous and strongly adapted process is progressively measurable. -/
theorem StronglyAdapted.progMeasurable_of_continuous [TopologicalSpace Î¹] [MetrizableSpace Î¹]
    [SecondCountableTopology Î¹] [MeasurableSpace Î¹] [OpensMeasurableSpace Î¹]
    [PseudoMetrizableSpace Î²] (h : StronglyAdapted f u) (hu_cont : âˆ€ Ï‰, Continuous fun i => u i Ï‰) :
    ProgMeasurable f u := fun i =>
  @stronglyMeasurable_uncurry_of_continuous_of_stronglyMeasurable _ _ (Set.Iic i) _ _ _ _ _ _ _
    (f i) _ (fun Ï‰ => (hu_cont Ï‰).comp continuous_induced_dom) fun j => (h j).mono (f.mono j.prop)

/-- For filtrations indexed by a discrete order, `StronglyAdapted` and `ProgMeasurable` are
equivalent. This lemma provides `StronglyAdapted f u â†’ ProgMeasurable f u`.
See `ProgMeasurable.stronglyAdapted` for the reverse direction, which is true more generally. -/
theorem StronglyAdapted.progMeasurable_of_discrete [TopologicalSpace Î¹] [DiscreteTopology Î¹]
    [SecondCountableTopology Î¹] [MeasurableSpace Î¹] [OpensMeasurableSpace Î¹]
    [PseudoMetrizableSpace Î²] (h : StronglyAdapted f u) : ProgMeasurable f u :=
  h.progMeasurable_of_continuous fun _ => continuous_of_discreteTopology

-- this dot notation will make more sense once we have a more general definition for predictable
theorem Predictable.stronglyAdapted {f : Filtration â„• m} {u : â„• â†’ Î© â†’ Î²}
    (hu : StronglyAdapted f fun n => u (n + 1)) (hu0 : StronglyMeasurable[f 0] (u 0)) :
    StronglyAdapted f u := fun n =>
  match n with
  | 0 => hu0
  | n + 1 => (hu n).mono (f.mono n.le_succ)

end MeasureTheory
