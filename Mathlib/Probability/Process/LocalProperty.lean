/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Kexing Ying
-/
import Mathlib.Probability.Process.Stopping

/-! # Local properties of processes

-/

open MeasureTheory Filter
open scoped ENNReal Topology

namespace ProbabilityTheory

variable {Î¹ Î© E : Type*} {mÎ© : MeasurableSpace Î©} {P : Measure Î©}

section Preorder

variable [Preorder Î¹]

/-- A localizing sequence is a sequence of stopping times that tends almost surely to infinity. -/
structure IsPreLocalizingSequence [TopologicalSpace Î¹] [OrderTopology Î¹]
    (ğ“• : Filtration Î¹ mÎ©) (Ï„ : â„• â†’ Î© â†’ WithTop Î¹) (P : Measure Î© := by volume_tac) :
    Prop where
  isStoppingTime : âˆ€ n, IsStoppingTime ğ“• (Ï„ n)
  tendsto_top : âˆ€áµ Ï‰ âˆ‚P, Tendsto (Ï„ Â· Ï‰) atTop (ğ“ âŠ¤)

/-- A localizing sequence is a sequence of stopping times that is almost surely increasing and
tends almost surely to infinity. -/
structure IsLocalizingSequence [TopologicalSpace Î¹] [OrderTopology Î¹]
    (ğ“• : Filtration Î¹ mÎ©) (Ï„ : â„• â†’ Î© â†’ WithTop Î¹)
    (P : Measure Î© := by volume_tac) extends IsPreLocalizingSequence ğ“• Ï„ P where
  mono : âˆ€áµ Ï‰ âˆ‚P, Monotone (Ï„ Â· Ï‰)

lemma isLocalizingSequence_const_top [TopologicalSpace Î¹] [OrderTopology Î¹]
    (ğ“• : Filtration Î¹ mÎ©) (P : Measure Î©) : IsLocalizingSequence ğ“• (fun _ _ â†¦ âŠ¤) P where
  isStoppingTime n := by simp [IsStoppingTime]
  mono := ae_of_all _ fun _ _ _ _ â†¦ by simp
  tendsto_top := by filter_upwards [] with Ï‰ using tendsto_const_nhds

end Preorder

section LinearOrder

variable [LinearOrder Î¹] {ğ“• : Filtration Î¹ mÎ©} {X : Î¹ â†’ Î© â†’ E} {p q : (Î¹ â†’ Î© â†’ E) â†’ Prop}

lemma IsLocalizingSequence.min [TopologicalSpace Î¹] [OrderTopology Î¹] {Ï„ Ïƒ : â„• â†’ Î© â†’ WithTop Î¹}
    (hÏ„ : IsLocalizingSequence ğ“• Ï„ P) (hÏƒ : IsLocalizingSequence ğ“• Ïƒ P) :
    IsLocalizingSequence ğ“• (min Ï„ Ïƒ) P where
  isStoppingTime n := (hÏ„.isStoppingTime n).min (hÏƒ.isStoppingTime n)
  mono := by filter_upwards [hÏ„.mono, hÏƒ.mono] with Ï‰ hÏ„Ï‰ hÏƒÏ‰; exact hÏ„Ï‰.min hÏƒÏ‰
  tendsto_top := by
    filter_upwards [hÏ„.tendsto_top, hÏƒ.tendsto_top] with Ï‰ hÏ„Ï‰ hÏƒÏ‰ using hÏ„Ï‰.min hÏƒÏ‰

variable [OrderBot Î¹]

@[simp]
lemma stoppedProcess_const_top : stoppedProcess X (fun _ â†¦ âŠ¤) = X := by
  unfold stoppedProcess
  simp

/-- A stochastic process `X` is said to satisfy a property `p` locally with respect to a
filtration `ğ“•` if there exists a localizing sequence `(Ï„_n)` such that for all `n`, the stopped
process of `fun i â†¦ {Ï‰ | âŠ¥ < Ï„ n Ï‰}.indicator (X i)` satisfies `p`. -/
def Locally [TopologicalSpace Î¹] [OrderTopology Î¹] [Zero E]
    (p : (Î¹ â†’ Î© â†’ E) â†’ Prop) (ğ“• : Filtration Î¹ mÎ©)
    (X : Î¹ â†’ Î© â†’ E) (P : Measure Î© := by volume_tac) : Prop :=
  âˆƒ Ï„ : â„• â†’ Î© â†’ WithTop Î¹, IsLocalizingSequence ğ“• Ï„ P âˆ§
    âˆ€ n, p (stoppedProcess (fun i â†¦ {Ï‰ | âŠ¥ < Ï„ n Ï‰}.indicator (X i)) (Ï„ n))

section Locally

variable [TopologicalSpace Î¹] [OrderTopology Î¹]

/-- A localizing sequence, witness of the local property of the stochastic process. -/
noncomputable
def Locally.localSeq [Zero E] (hX : Locally p ğ“• X P) :
    â„• â†’ Î© â†’ WithTop Î¹ :=
  hX.choose

lemma Locally.IsLocalizingSequence [Zero E] (hX : Locally p ğ“• X P) :
    IsLocalizingSequence ğ“• (hX.localSeq) P :=
  hX.choose_spec.1

lemma Locally.stoppedProcess [Zero E] (hX : Locally p ğ“• X P) (n : â„•) :
    p (stoppedProcess (fun i â†¦ {Ï‰ | âŠ¥ < hX.localSeq n Ï‰}.indicator (X i)) (hX.localSeq n)) :=
  hX.choose_spec.2 n

lemma locally_of_prop [Zero E] (hp : p X) : Locally p ğ“• X P :=
  âŸ¨fun n _ â†¦ (âŠ¤ : WithTop Î¹), isLocalizingSequence_const_top _ _, by simpaâŸ©

lemma Locally.mono [Zero E] (hpq : âˆ€ X, p X â†’ q X) (hpX : Locally p ğ“• X P) :
    Locally q ğ“• X P :=
  âŸ¨hpX.localSeq, hpX.IsLocalizingSequence, fun n â†¦ hpq _ <| hpX.stoppedProcess nâŸ©

lemma Locally.of_and [Zero E] (hX : Locally (fun Y â†¦ p Y âˆ§ q Y) ğ“• X P) :
    Locally p ğ“• X P âˆ§ Locally q ğ“• X P :=
  âŸ¨hX.mono <| fun _ â†¦ And.left, hX.mono <| fun _ â†¦ And.rightâŸ©

lemma Locally.of_and_left [Zero E] (hX : Locally (fun Y â†¦ p Y âˆ§ q Y) ğ“• X P) :
    Locally p ğ“• X P :=
  hX.of_and.left

lemma Locally.of_and_right [Zero E] (hX : Locally (fun Y â†¦ p Y âˆ§ q Y) ğ“• X P) :
    Locally q ğ“• X P :=
  hX.of_and.right

end Locally

/-- A property of stochastic processes is said to be stable if it is preserved under taking
the stopped process by a stopping time. -/
def IsStable [Zero E]
    (ğ“• : Filtration Î¹ mÎ©) (p : (Î¹ â†’ Î© â†’ E) â†’ Prop) : Prop :=
    âˆ€ X : Î¹ â†’ Î© â†’ E, p X â†’ âˆ€ Ï„ : Î© â†’ WithTop Î¹, IsStoppingTime ğ“• Ï„ â†’
      p (stoppedProcess (fun i â†¦ {Ï‰ | âŠ¥ < Ï„ Ï‰}.indicator (X i)) Ï„)


variable [TopologicalSpace Î¹] [OrderTopology Î¹] [Zero E]

lemma IsStable.isStable_locally (hp : IsStable ğ“• p) :
    IsStable ğ“• (fun Y â†¦ Locally p ğ“• Y P) := by
  intro X hX Ï„ hÏ„
  refine âŸ¨hX.localSeq, hX.IsLocalizingSequence, fun n â†¦ ?_âŸ©
  simp_rw [â† stoppedProcess_indicator_comm', Set.indicator_indicator, Set.inter_comm,
    â† Set.indicator_indicator, stoppedProcess_stoppedProcess, inf_comm]
  rw [stoppedProcess_indicator_comm', â† stoppedProcess_stoppedProcess]
  exact hp _ (hX.stoppedProcess n) Ï„ hÏ„

lemma locally_and (hp : IsStable ğ“• p) (hq : IsStable ğ“• q) :
    Locally (fun Y â†¦ p Y âˆ§ q Y) ğ“• X P â†” Locally p ğ“• X P âˆ§ Locally q ğ“• X P := by
  refine âŸ¨Locally.of_and, fun âŸ¨hpX, hqXâŸ© â†¦
    âŸ¨_, hpX.IsLocalizingSequence.min hqX.IsLocalizingSequence, fun n â†¦ âŸ¨?_, ?_âŸ©âŸ©âŸ©
  Â· convert hp _ (hpX.stoppedProcess n) _ <| hqX.IsLocalizingSequence.isStoppingTime n using 1
    ext i Ï‰
    rw [stoppedProcess_indicator_comm]
    simp_rw [Pi.inf_apply, lt_inf_iff, inf_comm (hpX.localSeq n)]
    rw [â† stoppedProcess_stoppedProcess, â† stoppedProcess_indicator_comm,
      (_ : {Ï‰ | âŠ¥ < hpX.localSeq n Ï‰ âˆ§ âŠ¥ < hqX.localSeq n Ï‰}
        = {Ï‰ | âŠ¥ < hpX.localSeq n Ï‰} âˆ© {Ï‰ | âŠ¥ < hqX.localSeq n Ï‰}), Set.inter_comm]
    Â· simp_rw [â† Set.indicator_indicator]
      rfl
    Â· rfl
  Â· convert hq _ (hqX.stoppedProcess n) _ <| hpX.IsLocalizingSequence.isStoppingTime n using 1
    ext i Ï‰
    rw [stoppedProcess_indicator_comm]
    simp_rw [Pi.inf_apply, lt_inf_iff]
    rw [â† stoppedProcess_stoppedProcess, â† stoppedProcess_indicator_comm,
      (_ : {Ï‰ | âŠ¥ < hpX.localSeq n Ï‰ âˆ§ âŠ¥ < hqX.localSeq n Ï‰}
        = {Ï‰ | âŠ¥ < hpX.localSeq n Ï‰} âˆ© {Ï‰ | âŠ¥ < hqX.localSeq n Ï‰})]
    Â· simp_rw [â† Set.indicator_indicator]
      rfl
    Â· rfl

end LinearOrder

section ConditionallyCompleteLinearOrderBot

variable [ConditionallyCompleteLinearOrderBot Î¹] [TopologicalSpace Î¹] [OrderTopology Î¹]
  [DenselyOrdered Î¹] [FirstCountableTopology Î¹] [NoMaxOrder Î¹]
  {ğ“• : Filtration Î¹ mÎ©} {X : Î¹ â†’ Î© â†’ E} {p q : (Î¹ â†’ Î© â†’ E) â†’ Prop}

lemma measure_iInter_of_ae_antitone {Î¹ : Type*}
    [Countable Î¹] [Preorder Î¹] [IsDirected Î¹ fun (x1 x2 : Î¹) â†¦ x1 â‰¤ x2]
    {s : Î¹ â†’ Set Î©} (hs : âˆ€áµ Ï‰ âˆ‚P, Antitone (s Â· Ï‰))
    (hsm : âˆ€ (i : Î¹), MeasureTheory.NullMeasurableSet (s i) P) (hfin : âˆƒ (i : Î¹), P (s i) â‰  âŠ¤) :
    P (â‹‚ (i : Î¹), s i) = â¨… (i : Î¹), P (s i) := by
  set t : Î¹ â†’ Set Î© := fun i â†¦ â‹‚ j â‰¤ i, s j with ht
  have hst (i : Î¹) : s i =áµ[P] t i := by
    filter_upwards [hs] with Ï‰ hÏ‰
    suffices Ï‰ âˆˆ s i â†” Ï‰ âˆˆ t i by
      exact propext this
    simp only [ht, Set.mem_iInter]
    refine âŸ¨fun (h : s i Ï‰) j hj â†¦ ?_, fun h â†¦ h i le_rflâŸ©
    change s j Ï‰
    specialize hÏ‰ hj
    simp only [le_Prop_eq] at hÏ‰
    exact hÏ‰ h
  rw [measure_congr <| EventuallyEq.countable_iInter hst, Antitone.measure_iInter]
  Â· exact iInf_congr <| fun i â†¦ measure_congr <| (hst i).symm
  Â· intros i j hij
    simp only [ht]
    rw [(_ : â‹‚ k â‰¤ j, s k = (â‹‚ k â‰¤ i, s k) âˆ© (â‹‚ k âˆˆ {k | k â‰¤ j âˆ§ Â¬ k â‰¤ i}, s k))]
    Â· exact Set.inter_subset_left
    Â· ext Ï‰
      simp only [Set.mem_iInter, Set.mem_setOf_eq, Set.mem_inter_iff, and_imp]
      grind
  Â· exact fun _ â†¦ NullMeasurableSet.iInter <| fun j â†¦ NullMeasurableSet.iInter <| fun _ â†¦ hsm j
  Â· obtain âŸ¨i, hiâŸ© := hfin
    refine âŸ¨i, (lt_of_le_of_lt ?_ <| lt_top_iff_ne_top.2 hi).neâŸ©
    rw [measure_congr (hst i)]

lemma isLocalizingSequence_of_isPreLocalizingSequence
    {Ï„ : â„• â†’ Î© â†’ WithTop Î¹} (hğ“• : IsRightContinuous ğ“•) (hÏ„ : IsPreLocalizingSequence ğ“• Ï„ P) :
    IsLocalizingSequence ğ“• (fun i Ï‰ â†¦ â¨… j â‰¥ i, Ï„ j Ï‰) P where
  isStoppingTime (n : â„•) := IsStoppingTime.iInf {j | j â‰¥ n} hğ“• (fun j â†¦ hÏ„.isStoppingTime j)
  mono :=  ae_of_all _ <| fun Ï‰ n m hnm â†¦ iInf_le_iInf_of_subset <| fun k hk â†¦ hnm.trans hk
  tendsto_top := by
    filter_upwards [hÏ„.tendsto_top] with Ï‰ hÏ‰
    replace hÏ‰ := hÏ‰.liminf_eq
    rw [liminf_eq_iSup_iInf_of_nat] at hÏ‰
    rw [â† hÏ‰]
    refine tendsto_atTop_iSup ?_
    intro n m hnm
    simp only [ge_iff_le, le_iInf_iff, iInf_le_iff]
    intro k hk i hi
    grind

/-- A stable property holds locally `p` for `X` if there exists a pre-localizing sequence `Ï„` for
which the stopped process of `fun i â†¦ {Ï‰ | âŠ¥ < Ï„ n Ï‰}.indicator (X i)` satisfies `p`. -/
lemma locally_of_isPreLocalizingSequence [Zero E] {Ï„ : â„• â†’ Î© â†’ WithTop Î¹}
    (hp : IsStable ğ“• p) (hğ“• : IsRightContinuous ğ“•) (hÏ„ : IsPreLocalizingSequence ğ“• Ï„ P)
    (hpÏ„ : âˆ€ n, p (stoppedProcess (fun i â†¦ {Ï‰ | âŠ¥ < Ï„ n Ï‰}.indicator (X i)) (Ï„ n))) :
    Locally p ğ“• X P := by
  refine âŸ¨_, isLocalizingSequence_of_isPreLocalizingSequence hğ“• hÏ„, fun n â†¦ ?_âŸ©
  have := hp _ (hpÏ„ n) (fun Ï‰ â†¦ â¨… j â‰¥ n, Ï„ j Ï‰) <|
    (isLocalizingSequence_of_isPreLocalizingSequence hğ“• hÏ„).isStoppingTime n
  rw [stoppedProcess_indicator_comm', â† stoppedProcess_stoppedProcess_of_le_right
    (Ï„ := fun Ï‰ â†¦ Ï„ n Ï‰) (fun _ â†¦ (iInf_le _ n).trans <| iInf_le _ le_rfl),
    â† stoppedProcess_indicator_comm']
  convert this using 2
  ext i Ï‰
  rw [stoppedProcess_indicator_comm', Set.indicator_indicator]
  congr 1
  ext Ï‰'
  simp only [ge_iff_le, Set.mem_setOf_eq, Set.mem_inter_iff]
  exact âŸ¨fun h â†¦ âŸ¨h, lt_of_lt_of_le h <| (iInf_le _ n).trans (iInf_le _ le_rfl)âŸ©, fun h â†¦ h.1âŸ©

section

omit [DenselyOrdered Î¹] [FirstCountableTopology Î¹] [NoMaxOrder Î¹]
variable [SecondCountableTopology Î¹] [IsFiniteMeasure P]

lemma isPreLocalizingSequence_of_isLocalizingSequence_aux'
    {Ï„ : â„• â†’ Î© â†’ WithTop Î¹} {Ïƒ : â„• â†’ â„• â†’ Î© â†’ WithTop Î¹}
    (hÏ„ : IsLocalizingSequence ğ“• Ï„ P) (hÏƒ : âˆ€ n, IsLocalizingSequence ğ“• (Ïƒ n) P) :
    âˆƒ T : â„• â†’ Î¹, Tendsto T atTop atTop
      âˆ§ âˆ€ n, âˆƒ k, P {Ï‰ | Ïƒ n k Ï‰ < min (Ï„ n Ï‰) (T n)} â‰¤ (1 / 2) ^ n := by
  obtain âŸ¨T, -, hTâŸ© := Filter.exists_seq_monotone_tendsto_atTop_atTop Î¹
  refine âŸ¨T, hT, fun n â†¦ ?_âŸ©
  by_contra hn; push_neg at hn
  suffices (1 / 2) ^ n â‰¤ P (â‹‚ k : â„•, {Ï‰ | Ïƒ n k Ï‰ < min (Ï„ n Ï‰) (T n)}) by
    refine (by simp : Â¬ (1 / 2 : â„â‰¥0âˆ) ^ n â‰¤ 0) <| this.trans <| nonpos_iff_eq_zero.2 ?_
    rw [measure_eq_zero_iff_ae_notMem]
    filter_upwards [(hÏƒ n).tendsto_top] with Ï‰ hTop hmem
    rw [WithTop.tendsto_atTop_nhds_top_iff] at hTop
    simp only [Set.mem_iInter, Set.mem_setOf_eq] at hmem
    obtain âŸ¨N, hNâŸ© := hTop (T n)
    specialize hN N le_rfl
    specialize hmem N
    grind
  rw [measure_iInter_of_ae_antitone, le_iInf_iff]
  Â· exact fun k â†¦ (hn k).le
  Â· filter_upwards [(hÏƒ n).mono] with Ï‰ hÏ‰
    intros i j hij
    specialize hÏ‰ hij
    simp only [lt_inf_iff, le_Prop_eq] at *
    change Ïƒ n j Ï‰ < Ï„ n Ï‰ âˆ§ Ïƒ n j Ï‰ < T n â†’ Ïƒ n i Ï‰ < Ï„ n Ï‰ âˆ§ Ïƒ n i Ï‰ < T n
    grind
  Â· intro i
    refine MeasurableSet.nullMeasurableSet ?_
    have hMÏƒ := ((hÏƒ n).isStoppingTime i).measurable
    have hMÏ„ := (hÏ„.isStoppingTime n).measurable
    simp_rw [lt_inf_iff]
    rw [(_ : {Ï‰ | Ïƒ n i Ï‰ < Ï„ n Ï‰ âˆ§ Ïƒ n i Ï‰ < T n} = {Ï‰ | Ïƒ n i Ï‰ < Ï„ n Ï‰} âˆ© {Ï‰ | Ïƒ n i Ï‰ < T n})]
    Â· exact MeasurableSet.inter
        (measurableSet_lt ((hÏƒ n).isStoppingTime i).measurable' (hÏ„.isStoppingTime n).measurable')
        <| measurableSet_lt ((hÏƒ n).isStoppingTime i).measurable' measurable_const
    Â· rfl
  Â· exact âŸ¨0, measure_ne_top P _âŸ©

/-- Auxliary defintion for `isPreLocalizingSequence_of_isLocalizingSequence` which constructs a
strictly increasing sequence from a given sequence. -/
def mkStrictMonoAux (x : â„• â†’ â„•) : â„• â†’ â„•
| 0 => x 0
| n + 1 => max (x (n + 1)) (mkStrictMonoAux x n) + 1

lemma mkStrictMonoAux_strictMono (x : â„• â†’ â„•) : StrictMono (mkStrictMonoAux x) := by
  refine strictMono_nat_of_lt_succ <| fun n â†¦ ?_
  simp only [mkStrictMonoAux]
  exact lt_of_le_of_lt (le_max_right (x (n + 1)) _) (lt_add_one (max (x (n + 1)) _))

lemma le_mkStrictMonoAux (x : â„• â†’ â„•) : âˆ€ n, x n â‰¤ mkStrictMonoAux x n
| 0 => by simp [mkStrictMonoAux]
| n + 1 => by
    simp only [mkStrictMonoAux]
    exact (le_max_left (x (n + 1)) (mkStrictMonoAux x n)).trans <|
       Nat.le_add_right (max (x (n + 1)) (mkStrictMonoAux x n)) 1

lemma isPreLocalizingSequence_of_isLocalizingSequence_aux
    {Ï„ : â„• â†’ Î© â†’ WithTop Î¹} {Ïƒ : â„• â†’ â„• â†’ Î© â†’ WithTop Î¹}
    (hÏ„ : IsLocalizingSequence ğ“• Ï„ P) (hÏƒ : âˆ€ n, IsLocalizingSequence ğ“• (Ïƒ n) P) :
    âˆƒ nk : â„• â†’ â„•, StrictMono nk âˆ§ âˆƒ T : â„• â†’ Î¹, Tendsto T atTop atTop
      âˆ§ âˆ€ n, P {Ï‰ | Ïƒ n (nk n) Ï‰ < min (Ï„ n Ï‰) (T n)} â‰¤ (1 / 2) ^ n := by
  obtain âŸ¨T, hT, hâŸ© := isPreLocalizingSequence_of_isLocalizingSequence_aux' hÏ„ hÏƒ
  choose nk hnk using h
  refine âŸ¨mkStrictMonoAux nk, mkStrictMonoAux_strictMono nk, T, hT, fun n â†¦
    le_trans (EventuallyLE.measure_le ?_) (hnk n)âŸ©
  filter_upwards [(hÏƒ n).mono] with Ï‰ hÏ‰
  specialize hÏ‰ (le_mkStrictMonoAux nk n)
  simp only [lt_inf_iff, le_Prop_eq]
  change Ïƒ n (mkStrictMonoAux nk n) Ï‰ < Ï„ n Ï‰ âˆ§ Ïƒ n (mkStrictMonoAux nk n) Ï‰ < T n â†’
    Ïƒ n (nk n) Ï‰ < Ï„ n Ï‰ âˆ§ Ïƒ n (nk n) Ï‰ < T n
  grind

lemma isPreLocalizingSequence_of_isLocalizingSequence
    [NoMaxOrder Î¹] {Ï„ : â„• â†’ Î© â†’ WithTop Î¹} {Ïƒ : â„• â†’ â„• â†’ Î© â†’ WithTop Î¹}
    (hÏ„ : IsLocalizingSequence ğ“• Ï„ P) (hÏƒ : âˆ€ n, IsLocalizingSequence ğ“• (Ïƒ n) P) :
    âˆƒ nk : â„• â†’ â„•, StrictMono nk
      âˆ§ IsPreLocalizingSequence ğ“• (fun i Ï‰ â†¦ (Ï„ i Ï‰) âŠ“ (Ïƒ i (nk i) Ï‰)) P := by
  obtain âŸ¨nk, hnk, T, hT, hPâŸ© := isPreLocalizingSequence_of_isLocalizingSequence_aux hÏ„ hÏƒ
  refine âŸ¨nk, hnk, fun n â†¦ (hÏ„.isStoppingTime n).min ((hÏƒ _).isStoppingTime _), ?_âŸ©
  have : âˆ‘' n, P {Ï‰ | Ïƒ n (nk n) Ï‰ < min (Ï„ n Ï‰) (T n)} < âˆ :=
    lt_of_le_of_lt (ENNReal.summable.tsum_mono ENNReal.summable hP)
      (tsum_geometric_lt_top.2 <| by norm_num)
  have hÏ„Top := hÏ„.tendsto_top
  filter_upwards [ae_eventually_notMem this.ne, hÏ„Top] with Ï‰ hÏ‰ hÏ‰Ï„
  replace hT := hÏ‰Ï„.min hT.tendsto_withTop_atTop_nhds_top
  simp_rw [eventually_atTop, not_lt, â† eventually_atTop] at hÏ‰
  rw [min_self] at hT
  rw [â† min_self âŠ¤]
  refine hÏ‰Ï„.min <| tendsto_of_tendsto_of_tendsto_of_le_of_le' hT tendsto_const_nhds hÏ‰ ?_
  simp only [le_top, eventually_atTop, ge_iff_le, implies_true, exists_const]

variable [DenselyOrdered Î¹] [NoMaxOrder Î¹] [Zero E]

/-- A stable property holding locally is idempotent. -/
lemma locally_locally
    (hğ“• : IsRightContinuous ğ“•) (hp : IsStable ğ“• p) :
    Locally (fun Y â†¦ Locally p ğ“• Y P) ğ“• X P â†” Locally p ğ“• X P := by
  refine âŸ¨fun hL â†¦ ?_, fun hL â†¦ ?_âŸ©
  Â· have hLL := hL.stoppedProcess
    choose Ï„ hÏ„â‚ hÏ„â‚‚ using hLL
    obtain âŸ¨nk, hnk, hpreâŸ© := isPreLocalizingSequence_of_isLocalizingSequence
      hL.IsLocalizingSequence hÏ„â‚
    refine locally_of_isPreLocalizingSequence hp hğ“• hpre <| fun n â†¦ ?_
    specialize hÏ„â‚‚ n (nk n)
    convert hÏ„â‚‚ using 1
    ext i Ï‰
    rw [stoppedProcess_indicator_comm', stoppedProcess_indicator_comm',
      stoppedProcess_stoppedProcess, stoppedProcess_indicator_comm']
    simp only [lt_inf_iff, Set.indicator_indicator]
    congr 1
    Â· ext Ï‰'; simp only [And.comm, Set.mem_setOf_eq, Set.mem_inter_iff]
    Â· simp_rw [inf_comm]
      rfl
  Â· exact âŸ¨hL.localSeq, hL.IsLocalizingSequence, fun n â†¦ locally_of_prop <| hL.stoppedProcess nâŸ©

/-- If `p` implies `q` locally, then `p` locally implies `q` locally. -/
lemma locally_induction (hğ“• : IsRightContinuous ğ“•)
    (hpq : âˆ€ Y, p Y â†’ Locally q ğ“• Y P) (hq : IsStable ğ“• q) (hpX : Locally p ğ“• X P) :
    Locally q ğ“• X P :=
  (locally_locally hğ“• hq).1 <| hpX.mono hpq

end

end ConditionallyCompleteLinearOrderBot

end ProbabilityTheory
