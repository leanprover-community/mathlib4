import Mathlib.Stuff.Inertia
import Mathlib.NumberTheory.Cyclotomic.Rat
import Mathlib.NumberTheory.NumberField.Ideal.KummerDedekind
import Mathlib.Stuff.Factorization

set_option linter.style.header false

open NumberField Module NumberField.InfinitePlace Nat Real RingOfIntegers Finset Multiset
  IsCyclotomicExtension.Rat Polynomial cyclotomic UniqueFactorizationMonoid Ideal

variable {n : â„•+} {K : Type*} [Field K] [NumberField K] [IsCyclotomicExtension {n} â„š K]

local notation "M " K:70 => (4 / Ï€) ^ nrComplexPlaces K *
  ((finrank â„š K)! / (finrank â„š K) ^ (finrank â„š K) * âˆš|discr K|)

namespace IsCyclotomicExtension.Rat

local notation3 "Î¸" => (zeta_spec n â„š K).toInteger

variable (n K) in
lemma minpoly : minpoly â„¤ Î¸ = cyclotomic n â„¤ := by
  have := cyclotomic_eq_minpoly (zeta_spec n â„š K) (by norm_num)
  rw [â† (zeta_spec n â„š K).coe_toInteger] at this
  simpa [this] using (minpoly.algebraMap_eq RingOfIntegers.coe_injective Î¸).symm

variable [hn : Fact (Nat.Prime n)]

variable (n) in
lemma exponent : exponent Î¸ = 1 := by
  simp [exponent_eq_one_iff, â† ((zeta_spec n â„š K).integralPowerBasis').adjoin_gen_eq_top]

lemma ne_dvd_exponent (p : â„•) [hp : Fact p.Prime] : Â¬ (p âˆ£ RingOfIntegers.exponent Î¸) := by
  rw [exponent, dvd_one]
  exact hp.1.ne_one

variable (n)

theorem pid1 (h : âˆ€ p âˆˆ Finset.Icc 1 âŒŠ(M K)âŒ‹â‚Š, (hp : p.Prime) â†’ p â‰  n â†’
    haveI : Fact (p.Prime) := âŸ¨hpâŸ©
      âˆƒ P, âˆƒ hP : P âˆˆ monicFactorsMod Î¸ p, âŒŠ(M K)âŒ‹â‚Š < p ^ P.natDegree âˆ¨
        Submodule.IsPrincipal ((primesOverSpanEquivMonicFactorsMod (ne_dvd_exponent p)).symm
          âŸ¨P, hPâŸ©).1) : IsPrincipalIdealRing (ğ“ K) := by
  have : IsGalois â„š K := isGalois n â„š K
  refine blahgalois (exponent n) (fun p hple hp â†¦ ?_)
  have : Fact (p.Prime) := âŸ¨hpâŸ©
  by_cases hpn : p = n
  Â· let Q : â„¤[X] := X - 1
    have hQ : Q.map (Int.castRingHom (ZMod p)) âˆˆ monicFactorsMod Î¸ p := by
      simp only [Polynomial.map_sub, map_X, Polynomial.map_one, mem_toFinset, Q]
      refine (Polynomial.mem_normalizedFactors_iff ((Monic.map _ <|
        minpoly n K â–¸ monic â†‘n â„¤).ne_zero)).mpr âŸ¨irreducible_of_degree_eq_one (by compute_degree!),
        by monicity, âŸ¨(X - 1) ^ (p - 2), ?_âŸ©âŸ©
      simp only [minpoly n K, map_cyclotomic]
      rw [â† mul_one n, PNat.mul_coe, PNat.one_coe, â†pow_one (n : â„•), â† hpn,
        cyclotomic_mul_prime_pow_eq (ZMod p) hp.not_dvd_one one_pos]
      simp only [cyclotomic_one, pow_one, tsub_self, pow_zero]
      rw [â† pow_succ' (X - 1)]
      congr
      have := hp.two_le
      omega
    refine âŸ¨Q.map (Int.castRingHom (ZMod p)), hQ, ?_âŸ©
    right
    rw [primesOverSpanEquivMonicFactorsMod_symm_apply_eq_span (ne_dvd_exponent p) hQ]
    simp only [map_sub, aeval_X, map_one, Q]
    refine âŸ¨Î¸ - 1, le_antisymm (span_le.mpr <| fun x hx â†¦ ?_) (span_le.mpr ?_)âŸ©
    Â· rcases hx with rfl | rfl
      Â· subst hpn
        simp [mem_span_singleton, (zeta_spec n â„š K).toInteger_sub_one_dvd_prime']
      Â· exact subset_span (by simp)
    Â· simp only [Set.singleton_subset_iff, SetLike.mem_coe, Q]
      exact subset_span (by simp)
  Â· exact h p hple hp hpn

theorem pid2 (h : âˆ€ p âˆˆ Finset.Icc 1 âŒŠ(M K)âŒ‹â‚Š, (hp : p.Prime) â†’ p â‰  n â†’
    haveI : Fact (p.Prime) := âŸ¨hpâŸ©
      âˆƒ P : â„¤[X], P.Monic âˆ§ P.map (Int.castRingHom (ZMod p)) âˆˆ monicFactorsMod Î¸ p âˆ§
        (âŒŠ(M K)âŒ‹â‚Š < p ^ P.natDegree âˆ¨
          Submodule.IsPrincipal (span {â†‘p, aeval Î¸ P}))) : IsPrincipalIdealRing (ğ“ K) := by
    refine pid1 n (fun p hple hp hpn â†¦ ?_)
    have : Fact (p.Prime) := âŸ¨hpâŸ©
    obtain âŸ¨P, hPmo, hP, hMâŸ© := h p hple hp hpn
    refine âŸ¨P.map (Int.castRingHom (ZMod p)), hP, ?_âŸ©
    rcases hM with H | H
    Â· left
      convert H
      simp [hPmo.leadingCoeff]
    Â· right
      simpa [primesOverSpanEquivMonicFactorsMod_symm_apply_eq_span (ne_dvd_exponent p) hP]

theorem pid3 (h : âˆ€ p âˆˆ Finset.Icc 1 âŒŠ(M K)âŒ‹â‚Š, (hp : p.Prime) â†’ (hpn : p â‰  n) â†’
    haveI : Fact (p.Prime) := âŸ¨hpâŸ©
      âˆƒ P Q A : â„¤[X], P.Monic âˆ§ orderOf (ZMod.unitOfCoprime _ (uff hn.1 hpn)) = P.natDegree
      âˆ§ P * Q + p * A = cyclotomic n â„¤ âˆ§
        (âŒŠ(M K)âŒ‹â‚Š < p ^ P.natDegree âˆ¨
          Submodule.IsPrincipal (span {â†‘p, aeval Î¸ P}))) : IsPrincipalIdealRing (ğ“ K) := by
  refine pid2 n (fun p hple hp hpn â†¦ ?_)
  have : Fact (p.Prime) := âŸ¨hpâŸ©
  obtain âŸ¨P, Q, A, hPmo, hP, hQA, hMâŸ© := h p hple hp hpn
  have : P.map (Int.castRingHom (ZMod p)) âˆ£ cyclotomic n (ZMod p) := by
    refine âŸ¨Q.map (Int.castRingHom (ZMod p)), ?_âŸ©
    simp [â† map_cyclotomic n (Int.castRingHom (ZMod p)), â† hQA]
  refine âŸ¨P, hPmo, mem_toFinset.mpr <| (Polynomial.mem_normalizedFactors_iff
    (((minpoly.monic (isIntegral Î¸)).map _).ne_zero)).mpr âŸ¨?_, hPmo.map _,
    by simp [minpoly, â† hQA]âŸ©, hMâŸ©
  exact baz'' hn.1 hpn this <| by simp [â† hP, hPmo]

theorem pid4 (h : âˆ€ p âˆˆ Finset.Icc 1 âŒŠ(M K)âŒ‹â‚Š, (hp : p.Prime) â†’ (hpn : p â‰  n) â†’
    haveI : Fact (p.Prime) := âŸ¨hpâŸ©
      âˆƒ P Q A G Qp Rp QP RP C1 C2 : â„¤[X],
        P.Monic âˆ§ orderOf (ZMod.unitOfCoprime _ (uff hn.1 hpn)) = P.natDegree
          âˆ§ P * Q + p * A = cyclotomic n â„¤ âˆ§
          (âŒŠ(M K)âŒ‹â‚Š < p ^ P.natDegree âˆ¨
            (p = G * Qp + Rp * (cyclotomic n â„¤) âˆ§
             P = G * QP + RP * (cyclotomic n â„¤) âˆ§
             G = C1 * P + C2 * p ))) : IsPrincipalIdealRing (ğ“ K) := by
  refine pid3 n (fun p hple hp hpn â†¦ ?_)
  obtain âŸ¨P, Q, A, G, Qp, Rp, QP, RP, C1, C2, hPmo, hP, hQA, hMâŸ© := h p hple hp hpn
  refine âŸ¨P, Q, A, hPmo, hP, hQA, ?_âŸ©
  rcases hM with H | âŸ¨Hp, HP, HGâŸ©
  Â· left
    assumption
  Â· right
    refine âŸ¨aeval Î¸ G, le_antisymm (span_le.mpr <| fun x hx â†¦ ?_) (span_le.mpr ?_)âŸ©
    Â· rcases hx with rfl | rfl
      Â· simp only [submodule_span_eq, SetLike.mem_coe, mem_span_singleton]
        refine âŸ¨aeval Î¸ Qp, ?_âŸ©
        rw [â† aeval_mul, â† sub_eq_iff_eq_add.mpr Hp]
        simp [â† minpoly n K]
      Â· simp only [submodule_span_eq, SetLike.mem_coe, mem_span_singleton]
        refine âŸ¨aeval Î¸ QP, ?_âŸ©
        rw [â† aeval_mul, â† sub_eq_iff_eq_add.mpr HP]
        simp [â† minpoly n K]
    Â· simp only [Set.singleton_subset_iff, SetLike.mem_coe, HG, _root_.map_add, map_mul,
        map_natCast]
      exact add_mem (mul_mem_left _ _ (subset_span (by simp)))
        (mul_mem_left _ _ (subset_span (by simp)))

end IsCyclotomicExtension.Rat
