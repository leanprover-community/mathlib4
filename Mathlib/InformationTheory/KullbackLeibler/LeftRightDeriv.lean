/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Lorenzo Luccioli
-/
import Mathlib.Analysis.Calculus.Deriv.Add
import Mathlib.Analysis.Calculus.Deriv.Comp
import Mathlib.Analysis.Calculus.Deriv.Mul
import Mathlib.Analysis.RCLike.Basic

/-!
# Left and right derivatives of real functions

TODO: do we keep those definitions?

## Main definitions

* `rightDeriv f x`: right derivative of `f` at `x` if it exists, 0 otherwise.
  Defined as `derivWithin f (Ioi x) x`.

## Main statements

* `fooBar_unique`

-/


open Set Filter Topology

open scoped ENNReal NNReal

variable {f : â„ â†’ â„} {s : Set â„} {x y : â„}

lemma Filter.EventuallyEq.derivWithin_eq_nhds {ğ•œ F : Type*} [NontriviallyNormedField ğ•œ]
    [NormedAddCommGroup F] [NormedSpace ğ•œ F] {fâ‚ f : ğ•œ â†’ F} {x : ğ•œ} {s : Set ğ•œ}
    (h : fâ‚ =á¶ [ğ“ x] f) :
    derivWithin fâ‚ s x = derivWithin f s x := by
  simp_rw [derivWithin]
  rw [Filter.EventuallyEq.fderivWithin_eq_nhds h]




/-- The right derivative of a real function. -/
noncomputable
def rightDeriv (f : â„ â†’ â„) : â„ â†’ â„ := fun x â†¦ derivWithin f (Ioi x) x

lemma rightDeriv_def (f : â„ â†’ â„) (x : â„) : rightDeriv f x = derivWithin f (Ioi x) x := rfl

/-- The left derivative of a real function. -/
noncomputable
def leftDeriv (f : â„ â†’ â„) : â„ â†’ â„ := fun x â†¦ derivWithin f (Iio x) x

lemma leftDeriv_def (f : â„ â†’ â„) (x : â„) : leftDeriv f x = derivWithin f (Iio x) x := rfl

lemma rightDeriv_of_not_differentiableWithinAt (hf : Â¬ DifferentiableWithinAt â„ f (Ioi x) x) :
    rightDeriv f x = 0 := by
  rw [rightDeriv_def, derivWithin_zero_of_not_differentiableWithinAt hf]

lemma leftDeriv_of_not_differentiableWithinAt (hf : Â¬ DifferentiableWithinAt â„ f (Iio x) x) :
    leftDeriv f x = 0 := by
  rw [leftDeriv_def, derivWithin_zero_of_not_differentiableWithinAt hf]

lemma rightDeriv_eq_neg_leftDeriv_neg_apply (f : â„ â†’ â„) (x : â„) :
    rightDeriv f x = - leftDeriv (fun x â†¦ f (-x)) (-x) := by
  change rightDeriv f x = -leftDeriv (f âˆ˜ Neg.neg) (-x)
  have h_map : MapsTo Neg.neg (Iio (-x)) (Ioi x) := fun _ (h : _ < -x) â†¦ lt_neg_of_lt_neg h
  have h_map' : MapsTo Neg.neg (Ioi x) (Iio (-x)) := fun _ (h : x < _) â†¦ neg_lt_neg h
  by_cases hf_diff : DifferentiableWithinAt â„ f (Ioi x) x
  swap
  Â· rw [rightDeriv_def, leftDeriv_def, derivWithin_zero_of_not_differentiableWithinAt hf_diff,
      derivWithin_zero_of_not_differentiableWithinAt, neg_zero]
    contrapose! hf_diff
    convert DifferentiableWithinAt.comp x hf_diff ((differentiable_neg _).differentiableWithinAt)
      h_map' using 1
    simp [Function.comp_assoc]
  Â· simp_rw [leftDeriv]
    rw [derivWithin_comp _ ((neg_neg x).symm â–¸ hf_diff)
        (differentiable_neg _).differentiableWithinAt h_map,
      neg_neg, â† rightDeriv_def, derivWithin_neg _ _ (uniqueDiffWithinAt_Iio _)]
    simp only [mul_neg, mul_one, neg_neg]

lemma leftDeriv_eq_neg_rightDeriv_neg_apply (f : â„ â†’ â„) (x : â„) :
    leftDeriv f x = - rightDeriv (fun y â†¦ f (-y)) (-x) := by
  simp [rightDeriv_eq_neg_leftDeriv_neg_apply, Function.comp_assoc]

lemma rightDeriv_eq_neg_leftDeriv_neg (f : â„ â†’ â„) :
    rightDeriv f = fun x â†¦ - leftDeriv (fun y â†¦ f (-y)) (-x) := by
  ext x
  simp [rightDeriv_eq_neg_leftDeriv_neg_apply]

lemma leftDeriv_eq_neg_rightDeriv_neg (f : â„ â†’ â„) :
    leftDeriv f = fun x â†¦ - rightDeriv (fun y â†¦ f (-y)) (-x) := by
  ext x
  simp [leftDeriv_eq_neg_rightDeriv_neg_apply]

lemma rightDeriv_congr {f g : â„ â†’ â„} {x : â„} (h : f =á¶ [ğ“[>] x] g) (hx : f x = g x) :
    rightDeriv f x = rightDeriv g x := h.derivWithin_eq hx

lemma leftDeriv_congr {f g : â„ â†’ â„} {x : â„} (h : f =á¶ [ğ“[<] x] g) (hx : f x = g x) :
    leftDeriv f x = leftDeriv g x := h.derivWithin_eq hx

lemma Filter.EventuallyEq.rightDeriv_eq_nhds {g : â„ â†’ â„} (h : f =á¶ [ğ“ x] g) :
    rightDeriv f x = rightDeriv g x := h.derivWithin_eq_nhds

lemma Filter.EventuallyEq.leftDeriv_eq_nhds {g : â„ â†’ â„} (h : f =á¶ [ğ“ x] g) :
    leftDeriv f x = leftDeriv g x := h.derivWithin_eq_nhds

lemma rightDeriv_congr_atTop {g : â„ â†’ â„} (h : f =á¶ [atTop] g) :
    rightDeriv f =á¶ [atTop] rightDeriv g := by
  have h' : âˆ€á¶  x in atTop, f =á¶ [ğ“ x] g := by
    -- todo: replace by clean filter proof?
    simp only [Filter.EventuallyEq, eventually_atTop, ge_iff_le] at h âŠ¢
    obtain âŸ¨a, haâŸ© := h
    refine âŸ¨a + 1, fun b hab â†¦ ?_âŸ©
    have h_ge : âˆ€á¶  x in ğ“ b, a â‰¤ x := eventually_ge_nhds ((lt_add_one _).trans_le hab)
    filter_upwards [h_ge] using ha
  filter_upwards [h'] with a ha using ha.rightDeriv_eq_nhds

lemma HasDerivWithinAt.rightDeriv {f' : â„} (h : HasDerivWithinAt f f' (Ioi x) x) :
    rightDeriv f x = f' := h.derivWithin (uniqueDiffWithinAt_Ioi x)

lemma HasDerivWithinAt.leftDeriv {f' : â„} (h : HasDerivWithinAt f f' (Iio x) x) :
    leftDeriv f x = f' := h.derivWithin (uniqueDiffWithinAt_Iio x)

lemma HasDerivAt.rightDeriv {f' : â„} (h : HasDerivAt f f' x) :
    rightDeriv f x = f' := h.hasDerivWithinAt.rightDeriv

lemma HasDerivAt.leftDeriv {f' : â„} (h : HasDerivAt f f' x) :
    leftDeriv f x = f' := h.hasDerivWithinAt.leftDeriv

@[simp]
lemma rightDeriv_zero : rightDeriv 0 = 0 := by ext; simp [rightDeriv_def]

@[simp]
lemma rightDeriv_const (c : â„) : rightDeriv (fun _ â†¦ c) = 0 := by ext; simp [rightDeriv_def]

@[simp]
lemma leftDeriv_const (c : â„) : leftDeriv (fun _ â†¦ c) = 0 := by ext; simp [leftDeriv_def]

@[simp]
lemma rightDeriv_const_mul (a : â„) :
    rightDeriv (fun x â†¦ a * f x) = fun x â†¦ a * rightDeriv f x := by
  ext; exact derivWithin_const_mul_field a

@[simp]
lemma leftDeriv_const_mul (a : â„) :
    leftDeriv (fun x â†¦ a * f x) = fun x â†¦ a * leftDeriv f x := by
  simp_rw [leftDeriv_eq_neg_rightDeriv_neg, rightDeriv_const_mul, neg_mul_eq_mul_neg]

@[simp]
lemma rightDeriv_neg : rightDeriv (fun x â†¦ - f x) = fun x â†¦ - rightDeriv f x := by
  simp_rw [â† neg_one_mul (f _), rightDeriv_const_mul, neg_one_mul]

@[simp]
lemma leftDeriv_neg : leftDeriv (fun x â†¦ - f x) = fun x â†¦ - leftDeriv f x := by
  simp [leftDeriv_eq_neg_rightDeriv_neg]

@[simp]
lemma rightDeriv_id : rightDeriv id = fun _ â†¦ 1 := by
  ext x
  rw [rightDeriv_def, derivWithin_id _ _ (uniqueDiffWithinAt_Ioi x)]

@[simp]
lemma rightDeriv_id' : rightDeriv (fun x â†¦ x) = fun _ â†¦ 1 := rightDeriv_id

@[simp]
lemma leftDeriv_id : leftDeriv id = fun _ â†¦ 1 := by
  ext x
  rw [leftDeriv_def, derivWithin_id _ _ (uniqueDiffWithinAt_Iio x)]

@[simp]
lemma leftDeriv_id' : leftDeriv (fun x â†¦ x) = fun _ â†¦ 1 := leftDeriv_id

@[simp]
lemma rightDeriv_neg_id {y : â„} : rightDeriv (fun x â†¦ - x) y = - 1 := (hasDerivAt_neg _).rightDeriv

@[simp]
lemma leftDeriv_neg_id {y : â„} : leftDeriv (fun x â†¦ - x) y = - 1 := (hasDerivAt_neg _).leftDeriv

lemma rightDeriv_add_apply {f g : â„ â†’ â„} {x : â„} (hf : DifferentiableWithinAt â„ f (Ioi x) x)
    (hg : DifferentiableWithinAt â„ g (Ioi x) x) :
    rightDeriv (f + g) x = rightDeriv f x + rightDeriv g x := by
  simp_rw [rightDeriv_def, â† derivWithin_add hf hg]
  rfl

lemma rightDeriv_add_apply' {f g : â„ â†’ â„} {x : â„} (hf : DifferentiableWithinAt â„ f (Ioi x) x)
    (hg : DifferentiableWithinAt â„ g (Ioi x) x) :
    rightDeriv (fun x â†¦ f x + g x) x = rightDeriv f x + rightDeriv g x := by
  simp_rw [rightDeriv_def, â† derivWithin_add hf hg]

lemma rightDeriv_add {f g : â„ â†’ â„} (hf : âˆ€ x, DifferentiableWithinAt â„ f (Ioi x) x)
    (hg : âˆ€ x, DifferentiableWithinAt â„ g (Ioi x) x) :
    rightDeriv (f + g) = fun x â†¦ rightDeriv f x + rightDeriv g x := by
  ext x; exact rightDeriv_add_apply (hf x) (hg x)

lemma rightDeriv_add' {f g : â„ â†’ â„} (hf : âˆ€ x, DifferentiableWithinAt â„ f (Ioi x) x)
    (hg : âˆ€ x, DifferentiableWithinAt â„ g (Ioi x) x) :
    rightDeriv (fun x â†¦ f x + g x) = fun x â†¦ rightDeriv f x + rightDeriv g x := by
  simp_rw [â† Pi.add_apply f g, rightDeriv_add hf hg]

lemma leftDeriv_add_apply {f g : â„ â†’ â„} {x : â„} (hf : DifferentiableWithinAt â„ f (Iio x) x)
    (hg : DifferentiableWithinAt â„ g (Iio x) x) :
    leftDeriv (f + g) x = leftDeriv f x + leftDeriv g x := by
  simp_rw [leftDeriv_def, â† derivWithin_add hf hg]
  rfl

lemma leftDeriv_add_apply' {f g : â„ â†’ â„} {x : â„} (hf : DifferentiableWithinAt â„ f (Iio x) x)
    (hg : DifferentiableWithinAt â„ g (Iio x) x) :
    leftDeriv (fun x â†¦ f x + g x) x = leftDeriv f x + leftDeriv g x := by
  simp_rw [leftDeriv_def, â† derivWithin_add hf hg]

lemma leftDeriv_add {f g : â„ â†’ â„} (hf : âˆ€ x, DifferentiableWithinAt â„ f (Iio x) x)
    (hg : âˆ€ x, DifferentiableWithinAt â„ g (Iio x) x) :
    leftDeriv (f + g) = fun x â†¦ leftDeriv f x + leftDeriv g x := by
  ext x; exact leftDeriv_add_apply (hf x) (hg x)

lemma leftDeriv_add' {f g : â„ â†’ â„} (hf : âˆ€ x, DifferentiableWithinAt â„ f (Iio x) x)
    (hg : âˆ€ x, DifferentiableWithinAt â„ g (Iio x) x) :
    leftDeriv (fun x â†¦ f x + g x) = fun x â†¦ leftDeriv f x + leftDeriv g x := by
  simp_rw [â† Pi.add_apply f g, leftDeriv_add hf hg]

lemma rightDeriv_add_const_apply (hf : DifferentiableWithinAt â„ f (Ioi x) x) (c : â„) :
    rightDeriv (fun x â†¦ f x + c) x = rightDeriv f x := by
  rw [rightDeriv_add_apply' hf (differentiableWithinAt_const c), rightDeriv_const,
    Pi.zero_apply, add_zero]

lemma rightDeriv_add_const (hf : âˆ€ x, DifferentiableWithinAt â„ f (Ioi x) x) (c : â„) :
    rightDeriv (fun x â†¦ f x + c) = rightDeriv f := by
  ext x; exact rightDeriv_add_const_apply (hf x) c

lemma leftDeriv_add_const_apply (hf : DifferentiableWithinAt â„ f (Iio x) x) (c : â„) :
    leftDeriv (fun x â†¦ f x + c) x = leftDeriv f x := by
  rw [leftDeriv_add_apply' hf (differentiableWithinAt_const c), leftDeriv_const,
    Pi.zero_apply, add_zero]

lemma leftDeriv_add_const (hf : âˆ€ x, DifferentiableWithinAt â„ f (Iio x) x) (c : â„) :
    leftDeriv (fun x â†¦ f x + c) = leftDeriv f := by
  ext x; exact leftDeriv_add_const_apply (hf x) c

lemma rightDeriv_add_linear_apply (hf : DifferentiableWithinAt â„ f (Ioi x) x) (a : â„) :
    rightDeriv (fun x â†¦ f x + a * x) x = rightDeriv f x + a := by
  rw [rightDeriv_add_apply' hf (by fun_prop), rightDeriv_const_mul, rightDeriv_id']
  simp

lemma rightDeriv_add_linear (hf : âˆ€ x, DifferentiableWithinAt â„ f (Ioi x) x) (a : â„) :
    rightDeriv (fun x â†¦ f x + a * x) = rightDeriv f + fun _ â†¦ a := by
  ext x; exact rightDeriv_add_linear_apply (hf x) a

lemma leftDeriv_add_linear_apply (hf : DifferentiableWithinAt â„ f (Iio x) x) (a : â„) :
    leftDeriv (fun x â†¦ f x + a * x) x = leftDeriv f x + a := by
  rw [leftDeriv_add_apply' hf (by fun_prop), leftDeriv_const_mul, leftDeriv_id']
  simp

lemma leftDeriv_add_linear (hf : âˆ€ x, DifferentiableWithinAt â„ f (Iio x) x) (a : â„) :
    leftDeriv (fun x â†¦ f x + a * x) = leftDeriv f + fun _ â†¦ a := by
  ext x; exact leftDeriv_add_linear_apply (hf x) a
