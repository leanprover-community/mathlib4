/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
module

public import Mathlib.Probability.Decision.DeGrootInfo
public import Mathlib.Probability.Distributions.Gaussian.Real

/-!
# Total variation distance

## Main definitions

* `tvDist Î¼ Î½`: the total variation distance between two measures `Î¼` and `Î½`.

## Main statements

* `fooBar_unique`

-/

@[expose] public section

open MeasureTheory Bool

open scoped ENNReal

namespace MeasureTheory.Measure

variable {Î± Î² : Type*} {m mÎ± : MeasurableSpace Î±} {mÎ² : MeasurableSpace Î²} {Î¼ Î½ : Measure Î±}

lemma withDensity_mono_measure (h : Î¼ â‰¤ Î½) {f : Î± â†’ â„â‰¥0âˆ} : Î¼.withDensity f â‰¤ Î½.withDensity f := by
  refine Measure.le_intro fun s hs _ â†¦ ?_
  rw [withDensity_apply _ hs, withDensity_apply _ hs]
  gcongr

lemma rnDeriv_add_self_right (Î½ Î¼ : Measure Î±) [SigmaFinite Î¼] [SigmaFinite Î½] :
    Î½.rnDeriv (Î¼ + Î½) =áµ[Î½] fun x â†¦ (Î¼.rnDeriv Î½ x + 1)â»Â¹ := by
  have hÎ½_ac : Î½ â‰ª Î¼ + Î½ := by rw [add_comm]; exact rfl.absolutelyContinuous.add_right _
  filter_upwards [Î¼.rnDeriv_add' Î½ Î½, Î½.rnDeriv_self, Measure.inv_rnDeriv hÎ½_ac] with a h1 h2 h3
  rw [Pi.inv_apply, h1, Pi.add_apply, h2, inv_eq_iff_eq_inv] at h3
  rw [h3]

lemma rnDeriv_add_self_left (Î¼ Î½ : Measure Î±) [SigmaFinite Î¼] [SigmaFinite Î½] :
    Î¼.rnDeriv (Î¼ + Î½) =áµ[Î½] fun x â†¦ Î¼.rnDeriv Î½ x / (Î¼.rnDeriv Î½ x + 1) := by
  have h_add : (Î¼ + Î½).rnDeriv (Î¼ + Î½) =áµ[Î½] Î¼.rnDeriv (Î¼ + Î½) + Î½.rnDeriv (Î¼ + Î½) :=
    (ae_add_measure_iff.mp (Î¼.rnDeriv_add' Î½ (Î¼ + Î½))).2
  have h_one_add := (ae_add_measure_iff.mp (Î¼ + Î½).rnDeriv_self).2
  have : (Î¼.rnDeriv (Î¼ + Î½)) =áµ[Î½] fun x â†¦ 1 - (Î¼.rnDeriv Î½ x + 1)â»Â¹ := by
    filter_upwards [h_add, h_one_add, rnDeriv_add_self_right Î½ Î¼] with a h4 h5 h6
    rw [h5, Pi.add_apply] at h4
    nth_rewrite 1 [h4]
    simp [h6]
  filter_upwards [this, Î¼.rnDeriv_lt_top Î½] with a ha ha_lt_top
  rw [ha, div_eq_mul_inv]
  refine ENNReal.sub_eq_of_eq_add (by simp) ?_
  nth_rewrite 2 [â† one_mul (Î¼.rnDeriv Î½ a + 1)â»Â¹]
  have h := add_mul (Î¼.rnDeriv Î½ a) 1 (Î¼.rnDeriv Î½ a + 1)â»Â¹
  rwa [ENNReal.mul_inv_cancel] at h
  Â· simp
  Â· simp [ha_lt_top.ne]

lemma rnDeriv_eq_div (Î¼ Î½ : Measure Î±) [SigmaFinite Î¼] [SigmaFinite Î½] :
    Î¼.rnDeriv Î½ =áµ[Î½] fun x â†¦ Î¼.rnDeriv (Î¼ + Î½) x / Î½.rnDeriv (Î¼ + Î½) x := by
  filter_upwards [rnDeriv_add_self_right Î½ Î¼, rnDeriv_add_self_left Î¼ Î½, Î¼.rnDeriv_lt_top Î½]
      with a ha1 ha2 ha_lt_top
  rw [ha1, ha2, ENNReal.div_eq_inv_mul, inv_inv, ENNReal.div_eq_inv_mul, â† mul_assoc,
      ENNReal.mul_inv_cancel, one_mul]
  Â· simp
  Â· simp [ha_lt_top.ne]

end MeasureTheory.Measure

namespace ProbabilityTheory

variable {ğ“§ ğ“¨ : Type*} {mğ“§ : MeasurableSpace ğ“§} {mğ“¨ : MeasurableSpace ğ“¨}
  {Î¼ Î½ : Measure ğ“§}

lemma mutuallySingular_iff_rnDeriv_eq_zero [SigmaFinite Î¼] [SigmaFinite Î½] :
    Î¼ âŸ‚â‚˜ Î½ â†” âˆ€áµ x âˆ‚(Î¼ + Î½), Î¼.rnDeriv (Î¼ + Î½) x = 0 âˆ¨ Î½.rnDeriv (Î¼ + Î½) x = 0 := by
  have hÎ¼_ac : Î¼ â‰ª Î¼ + Î½ := (Measure.AbsolutelyContinuous.refl _).add_right _
  have hÎ½_ac : Î½ â‰ª Î¼ + Î½ := by
    rw [add_comm]
    exact (Measure.AbsolutelyContinuous.refl _).add_right _
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· rw [ae_add_measure_iff]
    suffices h1 : âˆ€ (Î¼ : Measure ğ“§) (Î½) [SigmaFinite Î¼] [SigmaFinite Î½] (h : Î¼ âŸ‚â‚˜ Î½),
        âˆ€áµ x âˆ‚Î½, (âˆ‚Î¼/âˆ‚(Î¼ + Î½)) x = 0 âˆ¨ (âˆ‚Î½/âˆ‚(Î¼ + Î½)) x = 0 by
      refine âŸ¨?_, h1 Î¼ Î½ hâŸ©
      rw [add_comm Î¼]
      convert h1 Î½ Î¼ h.symm using 2
      rw [_root_.or_comm]
    intro Î¼ Î½ _ _ h
    rw [â† Measure.rnDeriv_eq_zero] at h
    have hÎ½_ac : Î½ â‰ª Î¼ + Î½ := by
      rw [add_comm]
      exact (Measure.AbsolutelyContinuous.refl _).add_right _
    filter_upwards [Î¼.rnDeriv_eq_div Î½, h, Measure.rnDeriv_pos hÎ½_ac,
      hÎ½_ac (Measure.rnDeriv_ne_top Î½ (Î¼+ Î½))] with x hx_div hx_zero hx_pos hx_ne_top
    rw [hx_div] at hx_zero
    simp only [Pi.zero_apply, ENNReal.div_eq_zero_iff, hx_ne_top, _root_.or_false] at hx_zero
    simp [hx_zero]
  Â· rw [â† Measure.rnDeriv_eq_zero]
    filter_upwards [Î¼.rnDeriv_eq_div Î½, Measure.rnDeriv_pos hÎ½_ac, hÎ½_ac h]
      with x hx_div hx_pos hx_min
    rw [hx_div]
    simp only [Pi.zero_apply, ENNReal.div_eq_zero_iff]
    left
    simpa [hx_pos.ne'] using hx_min

lemma mutuallySingular_dirac_of_ne [MeasurableSingletonClass ğ“§] {x y : ğ“§} (h : x â‰  y) :
    Measure.dirac x âŸ‚â‚˜ Measure.dirac y := âŸ¨{y}, measurableSet_singleton y, by simp [h]âŸ©

/-- Total variation distance between two measures. -/
noncomputable def tvDist (Î¼ Î½ : Measure ğ“§) : â„ := (deGrootInfo Î¼ Î½ (boolMeasure 1 1)).toReal

instance : IsFiniteMeasure (boolMeasure 1 1) := by constructor; simp

@[simp] lemma tvDist_zero_left : tvDist (0 : Measure ğ“§) Î½ = 0 := by simp [tvDist]

@[simp] lemma tvDist_zero_right : tvDist Î¼ (0 : Measure ğ“§) = 0 := by simp [tvDist]

@[simp] lemma tvDist_self : tvDist Î¼ Î¼ = 0 := by simp [tvDist]

lemma tvDist_nonneg : 0 â‰¤ tvDist Î¼ Î½ := ENNReal.toReal_nonneg

lemma tvDist_comm : tvDist Î¼ Î½ = tvDist Î½ Î¼ := by
  rw [tvDist, tvDist, deGrootInfo_comm]
  simp

lemma tvDist_le [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½] :
    tvDist Î¼ Î½ â‰¤ min (Î¼.real .univ) (Î½.real .univ) := by
  rw [Measure.real, Measure.real, â† ENNReal.toReal_min (measure_ne_top _ _) (measure_ne_top _ _)]
  refine ENNReal.toReal_mono ?_ ?_
  Â· simp
  Â· have h := deGrootInfo_le_min (Î¼ := Î¼) (Î½ := Î½) (Ï€ := boolMeasure 1 1)
    simpa only [boolMeasure_apply_false, one_mul, boolMeasure_apply_true] using h

lemma tvDist_le_one [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î½] :
    tvDist Î¼ Î½ â‰¤ 1 := tvDist_le.trans_eq (by simp)

/-- **Data processing inequality** for the total variation distance. -/
lemma tvDist_comp_le (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼] (Îº : Kernel ğ“§ ğ“¨) [IsMarkovKernel Îº] :
    tvDist (Îº âˆ˜â‚˜ Î¼) (Îº âˆ˜â‚˜ Î½) â‰¤ tvDist Î¼ Î½ :=
  ENNReal.toReal_mono deGrootInfo_ne_top (deGrootInfo_comp_le _ _ _ _)

/-- **Data processing inequality** for the total variation distance. -/
lemma tvDist_map_le (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼] {f : ğ“§ â†’ ğ“¨} (hf : Measurable f) :
    tvDist (Î¼.map f) (Î½.map f) â‰¤ tvDist Î¼ Î½ :=
  ENNReal.toReal_mono deGrootInfo_ne_top (deGrootInfo_map_le _ _ _ hf)

lemma tvDist_eq_min_sub_lintegral {Î¶ : Measure ğ“§} [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    [SigmaFinite Î¶] (hÎ¼Î¶ : Î¼ â‰ª Î¶) (hÎ½Î¶ : Î½ â‰ª Î¶) :
    tvDist Î¼ Î½ = min (Î¼.real .univ) (Î½.real .univ)
      - (âˆ«â» x, min ((âˆ‚Î¼/âˆ‚Î¶) x) ((âˆ‚Î½/âˆ‚Î¶) x) âˆ‚Î¶).toReal := by
  have h := deGrootInfo_eq_min_sub_lintegral' (boolMeasure 1 1) hÎ¼Î¶ hÎ½Î¶
  simp only [boolMeasure_apply_false, one_mul, boolMeasure_apply_true] at h
  rw [tvDist, h, Measure.real, Measure.real,
    â† ENNReal.toReal_min (measure_ne_top _ _) (measure_ne_top _ _),
    ENNReal.toReal_sub_of_le _ (by simp)]
  calc âˆ«â» x, min ((âˆ‚Î¼/âˆ‚Î¶) x) ((âˆ‚Î½/âˆ‚Î¶) x) âˆ‚Î¶
  _ â‰¤ min (âˆ«â» x, (âˆ‚Î¼/âˆ‚Î¶) x âˆ‚Î¶) (âˆ«â» x, (âˆ‚Î½/âˆ‚Î¶) x âˆ‚Î¶) := by
    refine le_min ?_ ?_
    Â· exact lintegral_mono fun _ â†¦ min_le_left _ _
    Â· exact lintegral_mono fun _ â†¦ min_le_right _ _
  _ = min (Î¼ .univ) (Î½ .univ) := by
    rw [Measure.lintegral_rnDeriv hÎ¼Î¶, Measure.lintegral_rnDeriv hÎ½Î¶]

lemma tvDist_eq_one_sub_lintegral {Î¶ : Measure ğ“§} [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î½]
    [SigmaFinite Î¶] (hÎ¼Î¶ : Î¼ â‰ª Î¶) (hÎ½Î¶ : Î½ â‰ª Î¶) :
    tvDist Î¼ Î½ = 1 - (âˆ«â» x, min ((âˆ‚Î¼/âˆ‚Î¶) x) ((âˆ‚Î½/âˆ‚Î¶) x) âˆ‚Î¶).toReal := by
  simp [tvDist_eq_min_sub_lintegral hÎ¼Î¶ hÎ½Î¶]

lemma tvDist_eq_min_sub_integral' [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½] :
    tvDist Î¼ Î½ = min (Î¼.real .univ) (Î½.real .univ)
      - âˆ« x, min ((âˆ‚Î¼/âˆ‚(Î¼ + Î½)) x).toReal ((âˆ‚Î½/âˆ‚(Î¼ + Î½)) x).toReal âˆ‚(Î¼ + Î½) := by
  rw [tvDist, toReal_deGrootInfo_eq_min_sub_integral, add_comm Î¼]
  simp [Measure.real, boolKernel_comp_measure]

lemma tvDist_eq_one_sub_integral' [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î½] :
    tvDist Î¼ Î½ = 1 - âˆ« x, min ((âˆ‚Î¼/âˆ‚(Î¼ + Î½)) x).toReal ((âˆ‚Î½/âˆ‚(Î¼ + Î½)) x).toReal âˆ‚(Î¼ + Î½) := by
  simp [tvDist_eq_min_sub_integral']

lemma tvDist_eq_min_sub_iInf_measurableSet (Î¼ Î½ : Measure ğ“§) [IsFiniteMeasure Î¼]
    [IsFiniteMeasure Î½] :
    tvDist Î¼ Î½ = min (Î¼.real .univ) (Î½.real .univ)
      - â¨… (E : {E // MeasurableSet E}), Î¼.real E + Î½.real E.1á¶œ := by
  have h := deGrootInfo_eq_min_sub_iInf_measurableSet Î¼ Î½ (boolMeasure 1 1)
  simp only [boolMeasure_apply_false, one_mul, boolMeasure_apply_true] at h
  rw [tvDist, h, Measure.real, Measure.real,
    â† ENNReal.toReal_min (measure_ne_top _ _) (measure_ne_top _ _),
    ENNReal.toReal_sub_of_le _ (by simp)]
  Â· congr
    rw [iInf_subtype']
    rw [â† ENNReal.toReal_ofReal (r := â¨… (E : {E //  MeasurableSet E}), Î¼.real E + Î½.real E.1á¶œ)]
    swap; Â· exact le_ciInf fun E â†¦ by positivity
    simp_rw [ENNReal.ofReal_iInf]
    congr with E
    rw [ENNReal.ofReal_add (by positivity) (by positivity)]
    simp
  Â· simp only [le_inf_iff]
    constructor
    Â· exact (iInf_le _ .univ).trans (by simp)
    Â· exact (iInf_le _ âˆ…).trans (by simp)

lemma tvDist_eq_one_sub_iInf_measurableSet (Î¼ Î½ : Measure ğ“§) [IsProbabilityMeasure Î¼]
    [IsProbabilityMeasure Î½] :
    tvDist Î¼ Î½ = 1 - â¨… (E : {E // MeasurableSet E}), Î¼.real E + Î½.real E.1á¶œ := by
  simp [tvDist_eq_min_sub_iInf_measurableSet]

lemma tvDist_eq_iSup_measurableSet_of_measure_univ_le [IsFiniteMeasure Î¼]
    [IsFiniteMeasure Î½] (h : Î½ .univ â‰¤ Î¼ .univ) :
    tvDist Î¼ Î½ = (â¨† E, â¨† (_ : MeasurableSet E), Î½ E - Î¼ E).toReal := by
  rw [tvDist, deGrootInfo_eq_iSup_measurableSet_of_measure_univ_le]
  Â· simp
  Â· simpa

lemma tvDist_eq_iSup_measurableSet_of_measure_univ_le' [IsFiniteMeasure Î¼]
    [IsFiniteMeasure Î½] (h : Î¼ .univ â‰¤ Î½ .univ) :
    tvDist Î¼ Î½ = (â¨† E, â¨† (_ : MeasurableSet E), Î¼ E - Î½ E).toReal := by
  rw [tvDist, deGrootInfo_eq_iSup_measurableSet_of_measure_univ_le']
  Â· simp
  Â· simpa

lemma tvDist_eq_iSup_measurableSet [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î½] :
    tvDist Î¼ Î½ = (â¨† E, â¨† (_ : MeasurableSet E), Î½ E - Î¼ E).toReal :=
  tvDist_eq_iSup_measurableSet_of_measure_univ_le (by simp)

lemma tvDist_eq_iSup_measurableSet' [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î½] :
    tvDist Î¼ Î½ = (â¨† E, â¨† (_ : MeasurableSet E), Î¼ E - Î½ E).toReal :=
  tvDist_eq_iSup_measurableSet_of_measure_univ_le' (by simp)

lemma tvDist_eq_zero_of_le [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½] (hÎ½Î¼ : Î½ â‰¤ Î¼) :
    tvDist Î¼ Î½ = 0 := by
  rw [tvDist, ENNReal.toReal_eq_zero_iff]
  exact Or.inl <| deGrootInfo_eq_zero_of_le (by simpa)

/-- The total variation between two probability measures is zero iff the measures are equal. -/
@[simp]
lemma tvDist_eq_zero_iff [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î½] :
    tvDist Î¼ Î½ = 0 â†” Î¼ = Î½ := by
  rw [tvDist, ENNReal.toReal_eq_zero_iff]
  simp [deGrootInfo_ne_top, deGrootInfo_eq_zero_iff]

/-- The total variation between two probability measures is one iff the measures are mutually
singular. -/
lemma tvDist_eq_one_iff_mutuallySingular [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î½] :
    tvDist Î¼ Î½ = 1 â†” Î¼ âŸ‚â‚˜ Î½ := by
  rw [mutuallySingular_iff_rnDeriv_eq_zero]
  have hÎ¼_ac : Î¼ â‰ª Î¼ + Î½ := (Measure.AbsolutelyContinuous.refl _).add_right _
  have hÎ½_ac : Î½ â‰ª Î¼ + Î½ := by
    rw [add_comm]
    exact (Measure.AbsolutelyContinuous.refl _).add_right _
  rw [tvDist_eq_one_sub_lintegral (Î¶ := Î¼ + Î½) hÎ¼_ac hÎ½_ac, sub_eq_self,
      ENNReal.toReal_eq_zero_iff]
  have : âˆ«â» x, min ((âˆ‚Î¼/âˆ‚(Î¼ + Î½)) x) ((âˆ‚Î½/âˆ‚(Î¼ + Î½)) x) âˆ‚(Î¼ + Î½) â‰  âˆ := by
    refine ne_top_of_le_ne_top (b := 1) (by simp) ?_
    calc âˆ«â» x, min ((âˆ‚Î¼/âˆ‚(Î¼ + Î½)) x) ((âˆ‚Î½/âˆ‚(Î¼ + Î½)) x) âˆ‚(Î¼ + Î½)
    _ â‰¤ âˆ«â» x, (âˆ‚Î¼/âˆ‚(Î¼ + Î½)) x âˆ‚(Î¼ + Î½) := by
      gcongr with x
      exact min_le_left _ _
    _ â‰¤ Î¼ Set.univ := Measure.lintegral_rnDeriv_le
    _ = 1 := by simp
  simp only [this, _root_.or_false]
  rw [lintegral_eq_zero_iff (by fun_prop)]
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
    <;> filter_upwards [h]
    <;> simp_rw [Pi.zero_apply, â† bot_eq_zero, min_eq_bot, bot_eq_zero]
    <;> exact fun x hx â†¦ hx

/-- The total variation between two Dirac distributions at different points is one. -/
lemma tvDist_dirac_of_ne [MeasurableSingletonClass ğ“§] {x y : ğ“§} (h : x â‰  y) :
    tvDist (Measure.dirac x) (Measure.dirac y) = 1 := by
  rw [tvDist_eq_one_iff_mutuallySingular]
  exact mutuallySingular_dirac_of_ne h

lemma tvDist_eq_half_integral_abs_sub [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î½] :
    tvDist Î¼ Î½ = 2â»Â¹ * âˆ« x, |((âˆ‚Î¼/âˆ‚(Î¼ + Î½)) x).toReal - ((âˆ‚Î½/âˆ‚(Î¼ + Î½)) x).toReal| âˆ‚(Î¼ + Î½) := by
  rw [tvDist, toReal_deGrootInfo_eq_integral_abs_boolKernel, add_comm Î¼ Î½]
  simp [boolKernel_comp_measure, Measure.real]

-- the l.h.s. is the Hellinger distance squared
lemma hellinger_le_tvDist [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î½] :
    1 - âˆ« x, âˆš((âˆ‚Î¼/âˆ‚(Î¼ + Î½)) x).toReal * âˆš((âˆ‚Î½/âˆ‚(Î¼ + Î½)) x).toReal âˆ‚(Î¼ + Î½) â‰¤ tvDist Î¼ Î½ := by
  have h_le {a b : â„} (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) : (a - b) ^ 2 â‰¤ |a ^ 2 - b ^ 2| := by
    calc (a - b) ^ 2
    _ = |a - b| * |a - b| := by rw [â† pow_two, sq_abs]
    _ â‰¤ |a - b| * (a + b) := by
      gcongr
      wlog hab : a â‰¤ b
      Â· rw [abs_sub_comm, add_comm]
        exact this (Î¼ := Î¼) (Î½ := Î½) hb ha (by linarith)
      Â· rw [abs_sub_comm, abs_of_nonneg (sub_nonneg.mpr hab)]
        linarith
    _ = |a ^ 2 - b ^ 2| := by
      rw [sq_sub_sq, abs_mul, mul_comm, abs_of_nonneg (a := a + b) (by positivity)]
  have h_le_rnDeriv x : (âˆš((âˆ‚Î¼/âˆ‚(Î¼ + Î½)) x).toReal - âˆš((âˆ‚Î½/âˆ‚(Î¼ + Î½)) x).toReal) ^ 2
      â‰¤ |((âˆ‚Î¼/âˆ‚(Î¼ + Î½)) x).toReal - ((âˆ‚Î½/âˆ‚(Î¼ + Î½)) x).toReal| := by
    refine (h_le (by positivity) (by positivity)).trans_eq ?_
    rw [Real.sq_sqrt (by positivity), Real.sq_sqrt (by positivity)]
  calc 1 - âˆ« x, âˆš((âˆ‚Î¼/âˆ‚(Î¼ + Î½)) x).toReal * âˆš((âˆ‚Î½/âˆ‚(Î¼ + Î½)) x).toReal âˆ‚(Î¼ + Î½)
  _ = 2â»Â¹ * (1 + 1 - âˆ« x, 2 * âˆš((âˆ‚Î¼/âˆ‚(Î¼ + Î½)) x).toReal * âˆš((âˆ‚Î½/âˆ‚(Î¼ + Î½)) x).toReal âˆ‚(Î¼ + Î½)) := by
    simp_rw [mul_assoc]
    rw [integral_const_mul]
    ring
  _ = 2â»Â¹ * (âˆ« x, ((âˆ‚Î¼/âˆ‚(Î¼ + Î½)) x).toReal âˆ‚(Î¼ + Î½) + âˆ« x, ((âˆ‚Î½/âˆ‚(Î¼ + Î½)) x).toReal âˆ‚(Î¼ + Î½)
        - âˆ« x, 2 * âˆš((âˆ‚Î¼/âˆ‚(Î¼ + Î½)) x).toReal * âˆš((âˆ‚Î½/âˆ‚(Î¼ + Î½)) x).toReal âˆ‚(Î¼ + Î½)) := by
    congr
    Â· rw [Measure.integral_toReal_rnDeriv]
      Â· simp
      Â· exact (Measure.AbsolutelyContinuous.refl _).add_right _
    Â· rw [Measure.integral_toReal_rnDeriv]
      Â· simp
      Â· rw [add_comm]
        exact (Measure.AbsolutelyContinuous.refl _).add_right _
  _ = 2â»Â¹ * âˆ« x, (âˆš((âˆ‚Î¼/âˆ‚(Î¼ + Î½)) x).toReal - âˆš((âˆ‚Î½/âˆ‚(Î¼ + Î½)) x).toReal) ^ 2 âˆ‚(Î¼ + Î½) := by
    rw [â† integral_add (by fun_prop) (by fun_prop), â† integral_sub (by fun_prop)]
    swap
    Â· refine integrable_of_le_of_le (gâ‚ := fun _ â†¦ 0)
        (gâ‚‚ := fun x â†¦ ((âˆ‚Î¼/âˆ‚(Î¼ + Î½)) x).toReal + ((âˆ‚Î½/âˆ‚(Î¼ + Î½)) x).toReal) (by fun_prop) ?_ ?_
        (by fun_prop) (by fun_prop)
      Â· exact ae_of_all _ fun _ â†¦ by positivity
      Â· refine ae_of_all _ fun x â†¦ ?_
        simp only
        refine (two_mul_le_add_sq _ _).trans_eq ?_
        rw [Real.sq_sqrt (by positivity), Real.sq_sqrt (by positivity)]
    congr with x
    rw [sub_sq, Real.sq_sqrt (by positivity), Real.sq_sqrt (by positivity)]
    ring
  _ â‰¤ 2â»Â¹ * âˆ« x, |((âˆ‚Î¼/âˆ‚(Î¼ + Î½)) x).toReal - ((âˆ‚Î½/âˆ‚(Î¼ + Î½)) x).toReal| âˆ‚(Î¼ + Î½) := by
    gcongr
    Â· refine integrable_of_le_of_le (gâ‚ := fun _ â†¦ 0)
        (gâ‚‚ := fun x â†¦ |((âˆ‚Î¼/âˆ‚(Î¼ + Î½)) x).toReal - ((âˆ‚Î½/âˆ‚(Î¼ + Î½)) x).toReal|) (by fun_prop) ?_ ?_
        (by fun_prop) (by fun_prop)
      Â· exact ae_of_all _ fun _ â†¦ by positivity
      Â· exact ae_of_all _ h_le_rnDeriv
    Â· fun_prop
    intro x
    exact h_le_rnDeriv x
  _ = tvDist Î¼ Î½ := tvDist_eq_half_integral_abs_sub.symm

lemma hellinger_le_tvDist' {Î¶ : Measure ğ“§} [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î½]
    [SigmaFinite Î¶] (hÎ¼Î¶ : Î¼ â‰ª Î¶) (hÎ½Î¶ : Î½ â‰ª Î¶) :
    1 - âˆ« x, âˆš((âˆ‚Î¼/âˆ‚Î¶) x).toReal * âˆš((âˆ‚Î½/âˆ‚Î¶) x).toReal âˆ‚Î¶ â‰¤ tvDist Î¼ Î½ := by
  refine le_trans (le_of_eq ?_) hellinger_le_tvDist
  -- extract lemma
  simp only [sub_right_inj]
  symm
  calc âˆ« x, âˆš((âˆ‚Î¼/âˆ‚(Î¼ + Î½)) x).toReal * âˆš((âˆ‚Î½/âˆ‚(Î¼ + Î½)) x).toReal âˆ‚(Î¼ + Î½)
  _ = âˆ« x, ((âˆ‚(Î¼ + Î½)/âˆ‚Î¶) x).toReal
      * âˆš((âˆ‚Î¼/âˆ‚(Î¼ + Î½)) x).toReal * âˆš((âˆ‚Î½/âˆ‚(Î¼ + Î½)) x).toReal âˆ‚Î¶ := by
    rw [â† integral_rnDeriv_smul (Î¼ := Î¼ + Î½) (Î½ := Î¶)]
    Â· simp only [smul_eq_mul]
      simp_rw [mul_assoc]
    Â· exact Measure.AbsolutelyContinuous.add_left hÎ¼Î¶ hÎ½Î¶
  _ = âˆ« x, âˆš((âˆ‚Î¼/âˆ‚(Î¼ + Î½)) x * (âˆ‚(Î¼ + Î½)/âˆ‚Î¶) x).toReal
      * âˆš((âˆ‚Î½/âˆ‚(Î¼ + Î½)) x * (âˆ‚(Î¼ + Î½)/âˆ‚Î¶) x).toReal âˆ‚Î¶ := by
    congr with x
    simp only [ENNReal.toReal_mul, ENNReal.toReal_nonneg, Real.sqrt_mul]
    conv_lhs => rw [â† Real.sq_sqrt (x := ((âˆ‚(Î¼ + Î½)/âˆ‚Î¶) x).toReal) (by positivity)]
    ring
  _ = âˆ« x, âˆš((âˆ‚Î¼/âˆ‚Î¶) x).toReal * âˆš((âˆ‚Î½/âˆ‚Î¶) x).toReal âˆ‚Î¶ := by
    refine integral_congr_ae ?_
    have h1 := Measure.rnDeriv_mul_rnDeriv (Î¼ := Î¼) (Î½ := Î¼ + Î½) (Îº := Î¶) ?_
    swap; Â· exact (Measure.AbsolutelyContinuous.refl _).add_right _
    have h2 := Measure.rnDeriv_mul_rnDeriv (Î¼ := Î½) (Î½ := Î¼ + Î½) (Îº := Î¶) ?_
    swap
    Â· rw [add_comm]
      exact (Measure.AbsolutelyContinuous.refl _).add_right _
    filter_upwards [h1, h2] with x hx1 hx2
    simp only [Pi.mul_apply] at hx1 hx2
    rw [hx1, hx2]

open Real

-- todo: extract lemma about the hellinger dist
lemma one_sub_exp_le_tvDist_gaussianReal (Î¼â‚ Î¼â‚‚ : â„) :
    1 - exp (-((Î¼â‚ - Î¼â‚‚) ^ 2) / 8) â‰¤ tvDist (gaussianReal Î¼â‚ 1) (gaussianReal Î¼â‚‚ 1) := by
  refine le_trans (le_of_eq ?_) (hellinger_le_tvDist' (Î¶ := â„™) ?_ ?_)
  rotate_left
  Â· exact gaussianReal_absolutelyContinuous _ (by simp)
  Â· exact gaussianReal_absolutelyContinuous _ (by simp)
  simp only [sub_right_inj]
  symm
  calc âˆ« x, âˆš((âˆ‚gaussianReal Î¼â‚ 1/âˆ‚â„™) x).toReal * âˆš((âˆ‚gaussianReal Î¼â‚‚ 1/âˆ‚â„™) x).toReal
  _ = âˆ« x, âˆš(gaussianPDFReal Î¼â‚ 1 x) * âˆš(gaussianPDFReal Î¼â‚‚ 1 x) := by
    refine integral_congr_ae ?_
    filter_upwards [rnDeriv_gaussianReal Î¼â‚ 1, rnDeriv_gaussianReal Î¼â‚‚ 1] with x h1 h2
    rw [h1, h2, toReal_gaussianPDF, toReal_gaussianPDF]
  _ = âˆ« x, âˆš((âˆš(2 * Ï€))â»Â¹ * exp (- (x - Î¼â‚) ^ 2 / 2))
      * âˆš((âˆš(2 * Ï€))â»Â¹ * exp (- (x - Î¼â‚‚) ^ 2 / 2)) := by simp [gaussianPDFReal]
  _ = âˆ« x, (âˆš(2 * Ï€))â»Â¹ * âˆš(exp (- (x - Î¼â‚) ^ 2 / 2)) * âˆš(exp (- (x - Î¼â‚‚) ^ 2 / 2)) := by
    congr with x
    conv_rhs => rw [â† Real.sq_sqrt (x := âˆš(2 * Ï€)) (by positivity), â† inv_pow, â† sqrt_inv]
    simp
    ring
  _ = âˆ« x, (âˆš(2 * Ï€))â»Â¹ * exp (- (x - Î¼â‚) ^ 2 / 4 - (x - Î¼â‚‚) ^ 2 / 4) := by
    congr with x
    rw [mul_assoc]
    congr
    simp_rw [sqrt_eq_rpow, â† exp_mul, â† exp_add]
    ring_nf
  _ = âˆ« x, (âˆš(2 * Ï€))â»Â¹ * exp (- (x - (Î¼â‚ + Î¼â‚‚) / 2) ^ 2 / 2 - (Î¼â‚ - Î¼â‚‚) ^ 2 / 8) := by
    congr with x
    congr 2
    ring
  _ = exp (- (Î¼â‚ - Î¼â‚‚) ^ 2 / 8) * âˆ« x, gaussianPDFReal ((Î¼â‚ + Î¼â‚‚) / 2) 1 x := by
    simp_rw [sub_eq_add_neg, exp_add, â† sub_eq_add_neg, â† mul_assoc]
    rw [integral_mul_const, mul_comm (exp _), neg_div]
    congr with x
    simp [gaussianPDFReal]
  _ = exp (-(Î¼â‚ - Î¼â‚‚) ^ 2 / 8) := by
    rw [integral_gaussianPDFReal_eq_one _ (by simp), mul_one]

end ProbabilityTheory
