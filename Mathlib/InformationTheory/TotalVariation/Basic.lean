/-
Copyright (c) 2025 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import Mathlib.Probability.Decision.DeGrootInfo

/-!
# Total variation distance

## Main definitions

* `tvDist Î¼ Î½`: the total variation distance between two measures `Î¼` and `Î½`.

## Main statements

* `fooBar_unique`

-/

open MeasureTheory Bool

open scoped ENNReal

namespace ProbabilityTheory

variable {ð“§ ð“¨ : Type*} {mð“§ : MeasurableSpace ð“§} {mð“¨ : MeasurableSpace ð“¨}
  {Î¼ Î½ : Measure ð“§}

/-- Total variation distance between two measures. -/
noncomputable def tvDist (Î¼ Î½ : Measure ð“§) : â„ := (deGrootInfo Î¼ Î½ (boolMeasure 1 1)).toReal

instance : IsFiniteMeasure (boolMeasure 1 1) := by constructor; simp

@[simp] lemma tvDist_zero_left : tvDist (0 : Measure ð“§) Î½ = 0 := by simp [tvDist]

@[simp] lemma tvDist_zero_right : tvDist Î¼ (0 : Measure ð“§) = 0 := by simp [tvDist]

@[simp] lemma tvDist_self : tvDist Î¼ Î¼ = 0 := by simp [tvDist]

lemma tvDist_nonneg : 0 â‰¤ tvDist Î¼ Î½ := ENNReal.toReal_nonneg

lemma tvDist_comm : tvDist Î¼ Î½ = tvDist Î½ Î¼ := by
  rw [tvDist, deGrootInfo_comm]
  congr
  ext
  Â· simp only [boolMeasure_apply_false]
    rw [Measure.map_apply (by fun_prop) .of_discrete]
    have : not â»Â¹' {false} = {true} := by ext; simp
    simp [this]
  Â· simp only [boolMeasure_apply_true]
    rw [Measure.map_apply (by fun_prop) .of_discrete]
    have : not â»Â¹' {true} = {false} := by ext; simp
    simp [this]

lemma tvDist_le [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½] :
    tvDist Î¼ Î½ â‰¤ min (Î¼.real .univ) (Î½.real .univ) := by
  rw [Measure.real, Measure.real, â† ENNReal.toReal_min (measure_ne_top _ _) (measure_ne_top _ _)]
  refine ENNReal.toReal_mono ?_ ?_
  Â· simp
  Â· have h := deGrootInfo_le_min (Î¼ := Î¼) (Î½ := Î½) (Ï€ := boolMeasure 1 1)
    simpa only [boolMeasure_apply_false, one_mul, boolMeasure_apply_true] using h

/-- **Data processing inequality** for the total variation distance. -/
lemma tvDist_comp_le (Î¼ Î½ : Measure ð“§) [IsFiniteMeasure Î¼] (Îº : Kernel ð“§ ð“¨) [IsMarkovKernel Îº] :
    tvDist (Îº âˆ˜â‚˜ Î¼) (Îº âˆ˜â‚˜ Î½) â‰¤ tvDist Î¼ Î½ :=
  ENNReal.toReal_mono deGrootInfo_ne_top (deGrootInfo_comp_le _ _ _ _)

lemma tvDist_eq_min_sub_lintegral {Î¶ : Measure ð“§} [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½]
    [SigmaFinite Î¶] (hÎ¼Î¶ : Î¼ â‰ª Î¶) (hÎ½Î¶ : Î½ â‰ª Î¶) :
    tvDist Î¼ Î½ = min (Î¼.real .univ) (Î½.real .univ)
      - (âˆ«â» x, min ((âˆ‚Î¼/âˆ‚Î¶) x) ((âˆ‚Î½/âˆ‚Î¶) x) âˆ‚Î¶).toReal := by
  have h := deGrootInfo_eq_min_sub_lintegral' (boolMeasure 1 1) hÎ¼Î¶ hÎ½Î¶
  simp only [boolMeasure_apply_false, one_mul, boolMeasure_apply_true] at h
  rw [tvDist, h, Measure.real, Measure.real,
    â† ENNReal.toReal_min (measure_ne_top _ _) (measure_ne_top _ _),
    ENNReal.toReal_sub_of_le _ (by simp)]
  calc âˆ«â» x, min ((âˆ‚Î¼/âˆ‚Î¶) x) ((âˆ‚Î½/âˆ‚Î¶) x) âˆ‚Î¶
  _ â‰¤ min (âˆ«â» x, (âˆ‚Î¼/âˆ‚Î¶) x âˆ‚Î¶) (âˆ«â» x, (âˆ‚Î½/âˆ‚Î¶) x âˆ‚Î¶) := by
    refine le_min ?_ ?_
    Â· exact lintegral_mono fun _ â†¦ min_le_left _ _
    Â· exact lintegral_mono fun _ â†¦ min_le_right _ _
  _ = min (Î¼ .univ) (Î½ .univ) := by
    rw [Measure.lintegral_rnDeriv hÎ¼Î¶, Measure.lintegral_rnDeriv hÎ½Î¶]

lemma tvDist_eq_one_sub_lintegral {Î¶ : Measure ð“§} [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î½]
    [SigmaFinite Î¶] (hÎ¼Î¶ : Î¼ â‰ª Î¶) (hÎ½Î¶ : Î½ â‰ª Î¶) :
    tvDist Î¼ Î½ = 1 - (âˆ«â» x, min ((âˆ‚Î¼/âˆ‚Î¶) x) ((âˆ‚Î½/âˆ‚Î¶) x) âˆ‚Î¶).toReal := by
  simp [tvDist_eq_min_sub_lintegral hÎ¼Î¶ hÎ½Î¶]

lemma tvDist_eq_min_sub_iInf_measurableSet (Î¼ Î½ : Measure ð“§) [IsFiniteMeasure Î¼]
    [IsFiniteMeasure Î½] :
    tvDist Î¼ Î½ = min (Î¼.real .univ) (Î½.real .univ)
      - â¨… (E : {E // MeasurableSet E}), Î¼.real E + Î½.real E.1á¶œ := by
  have h := deGrootInfo_eq_min_sub_iInf_measurableSet Î¼ Î½ (boolMeasure 1 1)
  simp only [boolMeasure_apply_false, one_mul, boolMeasure_apply_true] at h
  rw [tvDist, h, Measure.real, Measure.real,
    â† ENNReal.toReal_min (measure_ne_top _ _) (measure_ne_top _ _),
    ENNReal.toReal_sub_of_le _ (by simp)]
  Â· congr
    rw [iInf_subtype']
    rw [â† ENNReal.toReal_ofReal (r := â¨… (E : {E //  MeasurableSet E}), Î¼.real E + Î½.real E.1á¶œ)]
    swap; Â· exact le_ciInf fun E â†¦ by positivity
    simp_rw [ENNReal.ofReal_iInf]
    congr with E
    rw [ENNReal.ofReal_add (by positivity) (by positivity)]
    simp
  Â· simp only [le_inf_iff]
    constructor
    Â· exact (iInf_le _ .univ).trans (by simp)
    Â· exact (iInf_le _ âˆ…).trans (by simp)

lemma tvDist_eq_one_sub_iInf_measurableSet (Î¼ Î½ : Measure ð“§) [IsProbabilityMeasure Î¼]
    [IsProbabilityMeasure Î½] :
    tvDist Î¼ Î½ = 1 - â¨… (E : {E // MeasurableSet E}), Î¼.real E + Î½.real E.1á¶œ := by
  simp [tvDist_eq_min_sub_iInf_measurableSet]

lemma tvDist_eq_iSup_measurableSet_of_measure_univ_le [IsFiniteMeasure Î¼]
    [IsFiniteMeasure Î½] (h : Î½ .univ â‰¤ Î¼ .univ) :
    tvDist Î¼ Î½ = (â¨† E, â¨† (_ : MeasurableSet E), Î½ E - Î¼ E).toReal := by
  rw [tvDist, deGrootInfo_eq_iSup_measurableSet_of_measure_univ_le]
  Â· simp
  Â· simpa

lemma tvDist_eq_iSup_measurableSet_of_measure_univ_le' [IsFiniteMeasure Î¼]
    [IsFiniteMeasure Î½] (h : Î¼ .univ â‰¤ Î½ .univ) :
    tvDist Î¼ Î½ = (â¨† E, â¨† (_ : MeasurableSet E), Î¼ E - Î½ E).toReal := by
  rw [tvDist, deGrootInfo_eq_iSup_measurableSet_of_measure_univ_le']
  Â· simp
  Â· simpa

-- todo: generalize this and the below to deGrootInfo
lemma tvDist_eq_zero_of_le [IsFiniteMeasure Î¼] [IsFiniteMeasure Î½] (hÎ½Î¼ : Î½ â‰¤ Î¼) :
    tvDist Î¼ Î½ = 0 := by
  have h_le s : Î½ s â‰¤ Î¼ s := hÎ½Î¼ s
  rw [tvDist_eq_iSup_measurableSet_of_measure_univ_le (h_le .univ)]
  simp only [ENNReal.toReal_eq_zero_iff, ENNReal.iSup_eq_zero, tsub_eq_zero_iff_le, h_le,
    implies_true, _root_.true_or]

-- todo: finite and equal measure for univ suffices
lemma Measure.eq_of_le_of_isProbabilityMeasure [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î½]
    (hÎ¼Î½ : Î¼ â‰¤ Î½) : Î¼ = Î½ := by
  ext s hs
  refine le_antisymm (hÎ¼Î½ s) ?_
  by_contra! h_lt
  have : Set.univ = s âˆª sá¶œ := by simp
  have h_disj : Disjoint s sá¶œ := Set.disjoint_compl_right_iff_subset.mpr subset_rfl
  have h_univ : Î½ .univ â‰¤ Î¼ .univ := by simp
  rw [this, measure_union h_disj hs.compl, measure_union h_disj hs.compl] at h_univ
  refine absurd h_univ ?_
  push_neg
  refine ENNReal.add_lt_add_of_lt_of_le (by finiteness) h_lt (hÎ¼Î½ sá¶œ)

lemma tvDist_eq_zero_iff [IsProbabilityMeasure Î¼] [IsProbabilityMeasure Î½] :
    tvDist Î¼ Î½ = 0 â†” Î¼ = Î½ := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ by simp [h]âŸ©
  refine Measure.eq_of_le_of_isProbabilityMeasure ?_
  refine Measure.le_intro fun s hs _ â†¦ ?_
  rw [tvDist_eq_iSup_measurableSet_of_measure_univ_le' (by simp)] at h
  simp only [ENNReal.toReal_eq_zero_iff, ENNReal.iSup_eq_zero, tsub_eq_zero_iff_le] at h
  cases h with
  | inl h => exact h s hs
  | inr h =>
    refine absurd h ?_
    refine ne_top_of_le_ne_top (b := Î¼ .univ) (by simp) ?_
    exact iSupâ‚‚_le fun E hE â†¦ tsub_le_self.trans (measure_mono (Set.subset_univ _))

end ProbabilityTheory
