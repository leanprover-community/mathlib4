/-
Copyright (c) 2025 Tomaz Mascarenhas. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Geoffrey Irving, Tomaz Mascarenhas, Eric Wieser
-/
import Mathlib.Algebra.Group.Pi.Basic
import Mathlib.Data.Set.Insert

/-!
# Definition of a model of computation based on oracles.

This module defines an abstraction of computation with an oracle, enabling the analysis of upper
bounds on the query complexity of algorithms. It also provides a `Monad` instance for it.

See `Archive/SumOracle.lean` for an example usage.

## References

* A stochastic special case in:
  https://github.com/girving/debate/blob/862fdb1cf55df0d541b802bdb1e672d724df6398/Comp/Oracle.lean
-/


namespace QueryComplexity

/-- A deterministic computation that make decisions by querying an oracle.

A computation is either a pure value or a query to an oracle with input `i : Î¹` returning `Ï‰ i`.
For computations with multiple oracles, a `Sigma` or `inductive` type can be used for `Î¹`. -/
inductive Comp (Î¹ : Type*) (Ï‰ : Î¹ â†’ Type*) (Î± : Type*) : Type _ where
  /-- A pure value without any oracle interaction. -/
  | protected pure : Î± â†’ Comp Î¹ Ï‰ Î±
  /-- A query to the oracle, and a computation to continue from its return value. -/
  | queryBind : (i : Î¹) â†’ (Ï‰ i â†’ Comp Î¹ Ï‰ Î±) â†’ Comp Î¹ Ï‰ Î±

namespace Comp

section monad
variable {Î¹ : Type*}
variable {Î± Î² : Type*}
variable {Ï‰ : Î¹ â†’ Type*}

instance : Pure (Comp Î¹ Ï‰) where pure := Comp.pure

@[simp]
lemma pure_eq : (.pure : Î± â†’ Comp Î¹ Ï‰ Î±) = pure := rfl

/-- A query to the oracle -/
def query (i : Î¹) : Comp Î¹ Ï‰ (Ï‰ i) := queryBind i pure

/-- The standard bind operation for `Comp` -/
protected def bind (f : Comp Î¹ Ï‰ Î±) (g : Î± â†’ Comp Î¹ Ï‰ Î²) : Comp Î¹ Ï‰ Î² :=
  match f with
  | .pure x => g x
  | .queryBind i f => .queryBind i fun b => (f b).bind g

instance : Bind (Comp Î¹ Ï‰) where bind := Comp.bind

@[simp]
lemma bind_eq {Î± Î²} : (Comp.bind : Comp Î¹ Ï‰ Î± â†’ _ â†’ Comp Î¹ Ï‰ Î²) = Bind.bind := rfl

/-- `queryBind` is an implementation detail. -/
@[simp]
theorem queryBind_eq (i : Î¹) (f : Ï‰ i â†’ Comp Î¹ Ï‰ Î±) :
    queryBind i f = (query i).bind f :=
  rfl

@[simp]
protected theorem bind_pure (a : Î±) (g : Î± â†’ Comp Î¹ Ï‰ Î²) :
    (pure a : Comp Î¹ Ï‰ Î±).bind g = g a := rfl

@[simp]
theorem bind_queryBind (i : Î¹) (f : Ï‰ i â†’ Comp Î¹ Ï‰ Î±) (g : Î± â†’ Comp Î¹ Ï‰ Î²) :
    (queryBind i f).bind g = .queryBind i fun b => (f b).bind g := rfl


/-- The standard map operation for `Comp` -/
protected def map (g : Î± â†’ Î²) (f : Comp Î¹ Ï‰ Î±) : Comp Î¹ Ï‰ Î² :=
  match f with
  | .pure x => pure (g x)
  | .queryBind i f => .queryBind i fun b => (f b).map g

instance : Functor (Comp Î¹ Ï‰) where map := Comp.map

@[simp]
lemma map_eq {Î± Î²} : (Comp.map : (Î± â†’ Î²) â†’ (Comp Î¹ Ï‰ Î± â†’ Comp Î¹ Ï‰ Î²)) = Functor.map := rfl

@[simp]
protected theorem map_queryBind (g : Î± â†’ Î²) (i : Î¹) (f : Ï‰ i â†’ Comp Î¹ Ï‰ Î±) :
    (queryBind i f).map g = .queryBind i fun b => (f b).map g := rfl

@[simp]
protected theorem map_pure (g : Î± â†’ Î²) (a : Î±) :
    (pure a : Comp Î¹ Ï‰ Î±).map g = pure (g a) := rfl

/-- `Comp` is a monad -/
instance : Monad (Comp Î¹ Ï‰) where
  bind := Comp.bind
  map := Comp.map

/-- `Comp` is a lawful monad -/
instance : LawfulMonad (Comp Î¹ Ï‰) := LawfulMonad.mk'
  (id_map := fun x => by
    simp only [instMonad, Function.comp_id]
    induction x with
    | pure _ => rfl
    | queryBind i f h => simp [h, â† queryBind_eq])
  (pure_bind := fun x f => rfl)
  (bind_assoc := fun x f g => by
    simp [â† bind_eq]
    induction x with
    | pure _ => rfl
    | queryBind i f h => simp only [bind_queryBind, h])
  (bind_pure_comp := fun g f => by
    simp [â† bind_eq, â† map_eq]
    induction f with
    | pure _ => rfl
    | queryBind i f h => simp only [bind_queryBind, h, Comp.map_queryBind])

/-- A copy of `bind_queryBind` for the universe-monomorphic `Bind.bind`. -/
@[simp]
theorem bind'_query {Î± Î²} (i : Î¹) (f : Ï‰ i â†’ Comp Î¹ Ï‰ Î±) (g : Î± â†’ Comp Î¹ Ï‰ Î²) :
    queryBind i f >>= g = .queryBind i fun b => f b >>= g := rfl

/-- A copy of `map_queryBind` for the universe-monomorphic `Functor.map`. -/
@[simp]
protected theorem map'_query {Î± Î²} (g : Î± â†’ Î²) (i : Î¹) (f : Ï‰ i â†’ Comp Î¹ Ï‰ Î±) :
    g <$> queryBind i f = .queryBind i fun b => g <$> f b := rfl

end monad

section run
universe u v

-- note that monad composition requires some universe monomorphism here
variable {Î¹ : Type u}
variable {Î± Î² : Type v}
variable {Ï‰ : Î¹ â†’ Type v}
variable {m : Type v â†’ Type*}
variable [Monad m]

/-- Execute `f` with the oracle `oracle`.

Some notable choices of `m`:
* `Id`, for pure oracles
* `StateT â„• m`, for oracles which count their invocations
* `PMF` for stochastic oracles. -/
def runM (f : Comp Î¹ Ï‰ Î±) (oracle : (i : Î¹) â†’ m (Ï‰ i)) : m Î± :=
  match f with
  | .pure x => pure x
  | .queryBind i f => oracle i >>= (f Â· |>.runM oracle)

@[simp]
theorem runM_pure (a : Î±) (oracle : (i : Î¹) â†’ m (Ï‰ i)) :
    runM (pure a : Comp Î¹ Ï‰ Î±) oracle = pure a := rfl

@[simp]
theorem runM_queryBind (i : Î¹) (f : Ï‰ i â†’ Comp Î¹ Ï‰ Î±) (oracle : (i : Î¹) â†’ m (Ï‰ i)) :
    runM (.queryBind i f : Comp Î¹ Ï‰ Î±) oracle = oracle i >>= (f Â· |>.runM oracle) := rfl

@[simp]
theorem runM_query [LawfulMonad m] (i : Î¹) (oracle : (i : Î¹) â†’ m (Ï‰ i)) :
    runM (.query i : Comp Î¹ Ï‰ (Ï‰ i)) oracle = oracle i :=
  bind_pure _

@[simp]
theorem runM_bind [LawfulMonad m] (f : Comp Î¹ Ï‰ Î±) (g : Î± â†’ Comp Î¹ Ï‰ Î²)
    (oracle : (i : Î¹) â†’ m (Ï‰ i)) :
    runM (f >>= g) oracle = runM f oracle >>= fun a => runM (g a) oracle := by
  induction f with
  | pure _ => simp
  | queryBind i f h => simp [h, â† queryBind_eq]

@[simp]
theorem runM_map [LawfulMonad m] (g : Î± â†’ Î²) (f : Comp Î¹ Ï‰ Î±) (oracle : (i : Î¹) â†’ m (Ï‰ i)) :
    runM (g <$> f) oracle = g <$> runM f oracle := by
  induction f with
  | pure _ => simp
  | queryBind i f h => simp [h]

section SimpleCost

variable {ğ•œ}

/-- Like `runM`, but produce a count, starting at `init`. -/
abbrev run (f : Comp Î¹ Ï‰ Î±) (oracle : (i : Î¹) â†’ Ï‰ i) (init := 0) : Î± Ã— â„• :=
  let (a, n) := Id.run <| StateT.run (s := (âŸ¨initâŸ© : ULift â„•)) (m := Id) <| f.runM fun i => do
    modify (fun âŸ¨nâŸ© => âŸ¨n + 1âŸ©)
    pure (oracle i)
  (a, n.down)


@[simp]
theorem run_pure (a : Î±) (oracle : (i : Î¹) â†’ Ï‰ i) (init) :
    run (pure a : Comp Î¹ Ï‰ Î±) oracle init = (a, init) := rfl

@[simp]
theorem run_queryBind (i : Î¹) (f : Ï‰ i â†’ Comp Î¹ Ï‰ Î±) (oracle : (i : Î¹) â†’ Ï‰ i) (init) :
    run (.queryBind i f : Comp Î¹ Ï‰ Î±) oracle init =
      let (a, c) := (f (oracle i)).run oracle init; (a, c + 1) := by
  simp only [run, Id.run, queryBind_eq, bind_eq, bind_pure_comp, runM_bind, runM_query,
    bind_map_left, StateT.run_bind, StateT.run_modify, Id.pure_eq, Id.bind_eq]
  induction f (oracle i) generalizing init with
  | pure a => simp
  | queryBind i f ih =>
    simp [ih]


@[simp]
theorem run_query [LawfulMonad m] (i : Î¹) (oracle : (i : Î¹) â†’ Ï‰ i) :
    run (.query i : Comp Î¹ Ï‰ (Ï‰ i)) oracle = (oracle i, 1) := rfl

@[simp]
theorem run_bind [LawfulMonad m] (f : Comp Î¹ Ï‰ Î±) (g : Î± â†’ Comp Î¹ Ï‰ Î²)
    (oracle : (i : Î¹) â†’ Ï‰ i) :
    run (f >>= g) oracle =
      let (a, c) := run f oracle;
      let (b, câ‚‚) := run (g a) oracle
      (b, c + câ‚‚) := by
  simp [Id.run]
  induction f with
  | pure _ => simp [run, Id.run]
  | queryBind i f h => simp [queryBind_eq]

@[simp]
theorem run_map [LawfulMonad m] (g : Î± â†’ Î²) (f : Comp Î¹ Ï‰ Î±) (oracle : (i : Î¹) â†’ Ï‰ i) :
    run (g <$> f) oracle = g <$> run f oracle := by
  induction f with
  | pure _ => simp
  | queryBind i f h => simp [h]


abbrev value (f : Comp Î¹ Ï‰ Î±) (oracle : (i : Î¹) â†’ Ï‰ i) : Î± :=
  (run f oracle).1

abbrev cost (f : Comp Î¹ Ï‰ Î±) (oracle : (i : Î¹) â†’ Ï‰ i) : â„• :=
  (run f oracle).2


end SimpleCost

end Comp

end QueryComplexity
