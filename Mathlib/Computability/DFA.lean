/-
Copyright (c) 2020 Fox Thomson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Fox Thomson
-/
import Mathlib.Data.Fintype.Card
import Mathlib.Computability.Language
import Mathlib.Tactic.NormNum

#align_import computability.DFA from "leanprover-community/mathlib"@"32253a1a1071173b33dc7d6a218cf722c6feb514"

/-!
# Deterministic Finite Automata

This file contains the definition of a Deterministic Finite Automaton (DFA), a state machine which
determines whether a string (implemented as a list over an arbitrary alphabet) is in a regular set
in linear time.
Note that this definition allows for Automaton with infinite states, a `Fintype` instance must be
supplied for true DFA's.
-/


open Computability

universe u v

-- Porting note: Required as `DFA` is used in mathlib3
set_option linter.uppercaseLean3 false

/-- A DFA is a set of states (`Ïƒ`), a transition function from state to state labelled by the
  alphabet (`step`), a starting state (`start`) and a set of acceptance states (`accept`). -/
structure DFA (Î± : Type u) (Ïƒ : Type v) where
  /-- A transition function from state to state labelled by the alphabet. -/
  step : Ïƒ â†’ Î± â†’ Ïƒ
  /-- Starting state. -/
  start : Ïƒ
  /-- Set of acceptance states. -/
  accept : Set Ïƒ
#align DFA DFA

namespace DFA

variable {Î± : Type u} {Ïƒ : Type v} (M : DFA Î± Ïƒ)

instance [Inhabited Ïƒ] : Inhabited (DFA Î± Ïƒ) :=
  âŸ¨DFA.mk (fun _ _ => default) default âˆ…âŸ©

/-- `M.evalFrom s x` evaluates `M` with input `x` starting from the state `s`. -/
def evalFrom (start : Ïƒ) : List Î± â†’ Ïƒ :=
  List.foldl M.step start
#align DFA.eval_from DFA.evalFrom

@[simp]
theorem evalFrom_nil (s : Ïƒ) : M.evalFrom s [] = s :=
  rfl
#align DFA.eval_from_nil DFA.evalFrom_nil

@[simp]
theorem evalFrom_singleton (s : Ïƒ) (a : Î±) : M.evalFrom s [a] = M.step s a :=
  rfl
#align DFA.eval_from_singleton DFA.evalFrom_singleton

@[simp]
theorem evalFrom_append_singleton (s : Ïƒ) (x : List Î±) (a : Î±) :
    M.evalFrom s (x ++ [a]) = M.step (M.evalFrom s x) a := by
  simp only [evalFrom, List.foldl_append, List.foldl_cons, List.foldl_nil]
  -- ğŸ‰ no goals
#align DFA.eval_from_append_singleton DFA.evalFrom_append_singleton

/-- `M.eval x` evaluates `M` with input `x` starting from the state `M.start`. -/
def eval : List Î± â†’ Ïƒ :=
  M.evalFrom M.start
#align DFA.eval DFA.eval

@[simp]
theorem eval_nil : M.eval [] = M.start :=
  rfl
#align DFA.eval_nil DFA.eval_nil

@[simp]
theorem eval_singleton (a : Î±) : M.eval [a] = M.step M.start a :=
  rfl
#align DFA.eval_singleton DFA.eval_singleton

@[simp]
theorem eval_append_singleton (x : List Î±) (a : Î±) : M.eval (x ++ [a]) = M.step (M.eval x) a :=
  evalFrom_append_singleton _ _ _ _
#align DFA.eval_append_singleton DFA.eval_append_singleton

theorem evalFrom_of_append (start : Ïƒ) (x y : List Î±) :
    M.evalFrom start (x ++ y) = M.evalFrom (M.evalFrom start x) y :=
  x.foldl_append _ _ y
#align DFA.eval_from_of_append DFA.evalFrom_of_append

/-- `M.accepts` is the language of `x` such that `M.eval x` is an accept state. -/
def accepts : Language Î± := {x | M.eval x âˆˆ M.accept}
#align DFA.accepts DFA.accepts

theorem mem_accepts (x : List Î±) : x âˆˆ M.accepts â†” M.evalFrom M.start x âˆˆ M.accept := by rfl
                                                                                         -- ğŸ‰ no goals
#align DFA.mem_accepts DFA.mem_accepts

theorem evalFrom_split [Fintype Ïƒ] {x : List Î±} {s t : Ïƒ} (hlen : Fintype.card Ïƒ â‰¤ x.length)
    (hx : M.evalFrom s x = t) :
    âˆƒ q a b c,
      x = a ++ b ++ c âˆ§
        a.length + b.length â‰¤ Fintype.card Ïƒ âˆ§
          b â‰  [] âˆ§ M.evalFrom s a = q âˆ§ M.evalFrom q b = q âˆ§ M.evalFrom q c = t := by
  obtain âŸ¨n, m, hneq, heqâŸ© :=
    Fintype.exists_ne_map_eq_of_card_lt
      (fun n : Fin (Fintype.card Ïƒ + 1) => M.evalFrom s (x.take n)) (by norm_num)
  wlog hle : (n : â„•) â‰¤ m
  -- âŠ¢ âˆƒ q a b c, x = a ++ b ++ c âˆ§ List.length a + List.length b â‰¤ Fintype.card Ïƒ  â€¦
  Â· exact this _ hlen hx _ _ hneq.symm heq.symm (le_of_not_le hle)
    -- ğŸ‰ no goals
  have hm : (m : â„•) â‰¤ Fintype.card Ïƒ := Fin.is_le m
  -- âŠ¢ âˆƒ q a b c, x = a ++ b ++ c âˆ§ List.length a + List.length b â‰¤ Fintype.card Ïƒ  â€¦
  refine'
    âŸ¨M.evalFrom s ((x.take m).take n), (x.take m).take n, (x.take m).drop n, x.drop m, _, _, _, by
      rfl, _âŸ©
  Â· rw [List.take_append_drop, List.take_append_drop]
    -- ğŸ‰ no goals
  Â· simp only [List.length_drop, List.length_take]
    -- âŠ¢ min (â†‘n) (min (â†‘m) (List.length x)) + (min (â†‘m) (List.length x) - â†‘n) â‰¤ Fint â€¦
    rw [min_eq_left (hm.trans hlen), min_eq_left hle, add_tsub_cancel_of_le hle]
    -- âŠ¢ â†‘m â‰¤ Fintype.card Ïƒ
    exact hm
    -- ğŸ‰ no goals
  Â· intro h
    -- âŠ¢ False
    have hlen' := congr_arg List.length h
    -- âŠ¢ False
    simp only [List.length_drop, List.length, List.length_take] at hlen'
    -- âŠ¢ False
    rw [min_eq_left, tsub_eq_zero_iff_le] at hlen'
    -- âŠ¢ False
    Â· apply hneq
      -- âŠ¢ n = m
      apply le_antisymm
      -- âŠ¢ n â‰¤ m
      assumption'
      -- ğŸ‰ no goals
    exact hm.trans hlen
    -- ğŸ‰ no goals
  have hq : M.evalFrom (M.evalFrom s ((x.take m).take n)) ((x.take m).drop n) =
      M.evalFrom s ((x.take m).take n) := by
    rw [List.take_take, min_eq_left hle, â† evalFrom_of_append, heq, â† min_eq_left hle, â†
      List.take_take, min_eq_left hle, List.take_append_drop]
  use hq
  -- âŠ¢ evalFrom M (evalFrom M s (List.take (â†‘n) (List.take (â†‘m) x))) (List.drop (â†‘m â€¦
  rwa [â† hq, â† evalFrom_of_append, â† evalFrom_of_append, â† List.append_assoc,
    List.take_append_drop, List.take_append_drop]
#align DFA.eval_from_split DFA.evalFrom_split

theorem evalFrom_of_pow {x y : List Î±} {s : Ïƒ} (hx : M.evalFrom s x = s)
    (hy : y âˆˆ ({x} : Language Î±)âˆ—) : M.evalFrom s y = s := by
  rw [Language.mem_kstar] at hy
  -- âŠ¢ evalFrom M s y = s
  rcases hy with âŸ¨S, rfl, hSâŸ©
  -- âŠ¢ evalFrom M s (List.join S) = s
  induction' S with a S ih
  -- âŠ¢ evalFrom M s (List.join []) = s
  Â· rfl
    -- ğŸ‰ no goals
  Â· have ha := hS a (List.mem_cons_self _ _)
    -- âŠ¢ evalFrom M s (List.join (a :: S)) = s
    rw [Set.mem_singleton_iff] at ha
    -- âŠ¢ evalFrom M s (List.join (a :: S)) = s
    rw [List.join, evalFrom_of_append, ha, hx]
    -- âŠ¢ evalFrom M s (List.join S) = s
    apply ih
    -- âŠ¢ âˆ€ (y : List Î±), y âˆˆ S â†’ y âˆˆ {x}
    intro z hz
    -- âŠ¢ z âˆˆ {x}
    exact hS z (List.mem_cons_of_mem a hz)
    -- ğŸ‰ no goals
#align DFA.eval_from_of_pow DFA.evalFrom_of_pow

theorem pumping_lemma [Fintype Ïƒ] {x : List Î±} (hx : x âˆˆ M.accepts)
    (hlen : Fintype.card Ïƒ â‰¤ List.length x) :
    âˆƒ a b c,
      x = a ++ b ++ c âˆ§
        a.length + b.length â‰¤ Fintype.card Ïƒ âˆ§ b â‰  [] âˆ§ {a} * {b}âˆ— * {c} â‰¤ M.accepts := by
  obtain âŸ¨_, a, b, c, hx, hlen, hnil, rfl, hb, hcâŸ© := M.evalFrom_split hlen rfl
  -- âŠ¢ âˆƒ a b c, x = a ++ b ++ c âˆ§ List.length a + List.length b â‰¤ Fintype.card Ïƒ âˆ§  â€¦
  use a, b, c, hx, hlen, hnil
  -- âŠ¢ {a} * {b}âˆ— * {c} â‰¤ accepts M
  intro y hy
  -- âŠ¢ y âˆˆ accepts M
  rw [Language.mem_mul] at hy
  -- âŠ¢ y âˆˆ accepts M
  rcases hy with âŸ¨ab, c', hab, hc', rflâŸ©
  -- âŠ¢ ab ++ c' âˆˆ accepts M
  rw [Language.mem_mul] at hab
  -- âŠ¢ ab ++ c' âˆˆ accepts M
  rcases hab with âŸ¨a', b', ha', hb', rflâŸ©
  -- âŠ¢ a' ++ b' ++ c' âˆˆ accepts M
  rw [Set.mem_singleton_iff] at ha' hc'
  -- âŠ¢ a' ++ b' ++ c' âˆˆ accepts M
  substs ha' hc'
  -- âŠ¢ a' ++ b' ++ c' âˆˆ accepts M
  have h := M.evalFrom_of_pow hb hb'
  -- âŠ¢ a' ++ b' ++ c' âˆˆ accepts M
  rwa [mem_accepts, evalFrom_of_append, evalFrom_of_append, h, hc]
  -- ğŸ‰ no goals
#align DFA.pumping_lemma DFA.pumping_lemma

end DFA
