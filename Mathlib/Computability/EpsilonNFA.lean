/-
Copyright (c) 2021 Fox Thomson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Fox Thomson, YaÃ«l Dillies
-/
import Mathlib.Computability.NFA

#align_import computability.epsilon_NFA from "leanprover-community/mathlib"@"28aa996fc6fb4317f0083c4e6daf79878d81be33"

/-!
# Epsilon Nondeterministic Finite Automata

This file contains the definition of an epsilon Nondeterministic Finite Automaton (`ÎµNFA`), a state
machine which determines whether a string (implemented as a list over an arbitrary alphabet) is in a
regular set by evaluating the string over every possible path, also having access to Îµ-transitions,
which can be followed without reading a character.
Since this definition allows for automata with infinite states, a `Fintype` instance must be
supplied for true `ÎµNFA`'s.
-/


open Set

open Computability

-- "Îµ_NFA"
set_option linter.uppercaseLean3 false

universe u v

/-- An `ÎµNFA` is a set of states (`Ïƒ`), a transition function from state to state labelled by the
  alphabet (`step`), a starting state (`start`) and a set of acceptance states (`accept`).
  Note the transition function sends a state to a `Set` of states and can make Îµ-transitions by
  inputing `none`.
  Since this definition allows for Automata with infinite states, a `Fintype` instance must be
  supplied for true `ÎµNFA`'s. -/
structure ÎµNFA (Î± : Type u) (Ïƒ : Type v) where
  step : Ïƒ â†’ Option Î± â†’ Set Ïƒ
  start : Set Ïƒ
  accept : Set Ïƒ
#align Îµ_NFA ÎµNFA

variable {Î± : Type u} {Ïƒ Ïƒ' : Type v} (M : ÎµNFA Î± Ïƒ) {S : Set Ïƒ} {x : List Î±} {s : Ïƒ} {a : Î±}

namespace ÎµNFA

/-- The `ÎµClosure` of a set is the set of states which can be reached by taking a finite string of
Îµ-transitions from an element of the set. -/
inductive ÎµClosure (S : Set Ïƒ) : Set Ïƒ
  | base : âˆ€ s âˆˆ S, ÎµClosure S s
  | step : âˆ€ (s), âˆ€ t âˆˆ M.step s none, ÎµClosure S s â†’ ÎµClosure S t
#align Îµ_NFA.Îµ_closure ÎµNFA.ÎµClosure

@[simp]
theorem subset_ÎµClosure (S : Set Ïƒ) : S âŠ† M.ÎµClosure S :=
  ÎµClosure.base
#align Îµ_NFA.subset_Îµ_closure ÎµNFA.subset_ÎµClosure

@[simp]
theorem ÎµClosure_empty : M.ÎµClosure âˆ… = âˆ… :=
  eq_empty_of_forall_not_mem fun s hs â†¦ by induction hs <;> assumption
                                           -- âŠ¢ False
                                                            -- ðŸŽ‰ no goals
                                                            -- ðŸŽ‰ no goals
#align Îµ_NFA.Îµ_closure_empty ÎµNFA.ÎµClosure_empty

@[simp]
theorem ÎµClosure_univ : M.ÎµClosure univ = univ :=
  eq_univ_of_univ_subset <| subset_ÎµClosure _ _
#align Îµ_NFA.Îµ_closure_univ ÎµNFA.ÎµClosure_univ

/-- `M.stepSet S a` is the union of the Îµ-closure of `M.step s a` for all `s âˆˆ S`. -/
def stepSet (S : Set Ïƒ) (a : Î±) : Set Ïƒ :=
  â‹ƒ s âˆˆ S, M.ÎµClosure (M.step s a)
#align Îµ_NFA.step_set ÎµNFA.stepSet

variable {M}

@[simp]
theorem mem_stepSet_iff : s âˆˆ M.stepSet S a â†” âˆƒ t âˆˆ S, s âˆˆ M.ÎµClosure (M.step t a) := by
  simp_rw [stepSet, mem_iUnionâ‚‚, exists_prop]
  -- ðŸŽ‰ no goals
#align Îµ_NFA.mem_step_set_iff ÎµNFA.mem_stepSet_iff

@[simp]
theorem stepSet_empty (a : Î±) : M.stepSet âˆ… a = âˆ… := by
  simp_rw [stepSet, mem_empty_iff_false, iUnion_false, iUnion_empty]
  -- ðŸŽ‰ no goals
#align Îµ_NFA.step_set_empty ÎµNFA.stepSet_empty

variable (M)

/-- `M.evalFrom S x` computes all possible paths through `M` with input `x` starting at an element
of `S`. -/
def evalFrom (start : Set Ïƒ) : List Î± â†’ Set Ïƒ :=
  List.foldl M.stepSet (M.ÎµClosure start)
#align Îµ_NFA.eval_from ÎµNFA.evalFrom

@[simp]
theorem evalFrom_nil (S : Set Ïƒ) : M.evalFrom S [] = M.ÎµClosure S :=
  rfl
#align Îµ_NFA.eval_from_nil ÎµNFA.evalFrom_nil

@[simp]
theorem evalFrom_singleton (S : Set Ïƒ) (a : Î±) : M.evalFrom S [a] = M.stepSet (M.ÎµClosure S) a :=
  rfl
#align Îµ_NFA.eval_from_singleton ÎµNFA.evalFrom_singleton

@[simp]
theorem evalFrom_append_singleton (S : Set Ïƒ) (x : List Î±) (a : Î±) :
    M.evalFrom S (x ++ [a]) = M.stepSet (M.evalFrom S x) a := by
  rw [evalFrom, List.foldl_append, List.foldl_cons, List.foldl_nil]
  -- ðŸŽ‰ no goals
#align Îµ_NFA.eval_from_append_singleton ÎµNFA.evalFrom_append_singleton

@[simp]
theorem evalFrom_empty (x : List Î±) : M.evalFrom âˆ… x = âˆ… := by
  induction' x using List.reverseRecOn with x a ih
  -- âŠ¢ evalFrom M âˆ… [] = âˆ…
  Â· rw [evalFrom_nil, ÎµClosure_empty]
    -- ðŸŽ‰ no goals
  Â· rw [evalFrom_append_singleton, ih, stepSet_empty]
    -- ðŸŽ‰ no goals
#align Îµ_NFA.eval_from_empty ÎµNFA.evalFrom_empty

/-- `M.eval x` computes all possible paths through `M` with input `x` starting at an element of
`M.start`. -/
def eval :=
  M.evalFrom M.start
#align Îµ_NFA.eval ÎµNFA.eval

@[simp]
theorem eval_nil : M.eval [] = M.ÎµClosure M.start :=
  rfl
#align Îµ_NFA.eval_nil ÎµNFA.eval_nil

@[simp]
theorem eval_singleton (a : Î±) : M.eval [a] = M.stepSet (M.ÎµClosure M.start) a :=
  rfl
#align Îµ_NFA.eval_singleton ÎµNFA.eval_singleton

@[simp]
theorem eval_append_singleton (x : List Î±) (a : Î±) : M.eval (x ++ [a]) = M.stepSet (M.eval x) a :=
  evalFrom_append_singleton _ _ _ _
#align Îµ_NFA.eval_append_singleton ÎµNFA.eval_append_singleton

/-- `M.accepts` is the language of `x` such that there is an accept state in `M.eval x`. -/
def accepts : Language Î± :=
  { x | âˆƒ S âˆˆ M.accept, S âˆˆ M.eval x }
#align Îµ_NFA.accepts ÎµNFA.accepts

/-! ### Conversions between `ÎµNFA` and `NFA` -/


/-- `M.toNFA` is an `NFA` constructed from an `ÎµNFA` `M`. -/
def toNFA : NFA Î± Ïƒ where
  step S a := M.ÎµClosure (M.step S a)
  start := M.ÎµClosure M.start
  accept := M.accept
#align Îµ_NFA.to_NFA ÎµNFA.toNFA

@[simp]
theorem toNFA_evalFrom_match (start : Set Ïƒ) :
    M.toNFA.evalFrom (M.ÎµClosure start) = M.evalFrom start :=
  rfl
#align Îµ_NFA.to_NFA_eval_from_match ÎµNFA.toNFA_evalFrom_match

@[simp]
theorem toNFA_correct : M.toNFA.accepts = M.accepts :=
  rfl
#align Îµ_NFA.to_NFA_correct ÎµNFA.toNFA_correct

theorem pumping_lemma [Fintype Ïƒ] {x : List Î±} (hx : x âˆˆ M.accepts)
    (hlen : Fintype.card (Set Ïƒ) â‰¤ List.length x) :
    âˆƒ a b c, x = a ++ b ++ c âˆ§
      a.length + b.length â‰¤ Fintype.card (Set Ïƒ) âˆ§ b â‰  [] âˆ§ {a} * {b}âˆ— * {c} â‰¤ M.accepts :=
  M.toNFA.pumping_lemma hx hlen
#align Îµ_NFA.pumping_lemma ÎµNFA.pumping_lemma

end ÎµNFA

namespace NFA

/-- `M.toÎµNFA` is an `ÎµNFA` constructed from an `NFA` `M` by using the same start and accept
  states and transition functions. -/
def toÎµNFA (M : NFA Î± Ïƒ) : ÎµNFA Î± Ïƒ where
  step s a := a.casesOn' âˆ… fun a â†¦ M.step s a
  start := M.start
  accept := M.accept
#align NFA.to_Îµ_NFA NFA.toÎµNFA

@[simp]
theorem toÎµNFA_ÎµClosure (M : NFA Î± Ïƒ) (S : Set Ïƒ) : M.toÎµNFA.ÎµClosure S = S := by
  ext a
  -- âŠ¢ a âˆˆ ÎµNFA.ÎµClosure (toÎµNFA M) S â†” a âˆˆ S
  refine' âŸ¨_, ÎµNFA.ÎµClosure.base _âŸ©
  -- âŠ¢ a âˆˆ ÎµNFA.ÎµClosure (toÎµNFA M) S â†’ a âˆˆ S
  rintro (âŸ¨_, hâŸ© | âŸ¨_, _, h, _âŸ©)
  -- âŠ¢ a âˆˆ S
  Â· exact h
    -- ðŸŽ‰ no goals
  Â· cases h
    -- ðŸŽ‰ no goals
#align NFA.to_Îµ_NFA_Îµ_closure NFA.toÎµNFA_ÎµClosure

@[simp]
theorem toÎµNFA_evalFrom_match (M : NFA Î± Ïƒ) (start : Set Ïƒ) :
    M.toÎµNFA.evalFrom start = M.evalFrom start := by
  rw [evalFrom, ÎµNFA.evalFrom, toÎµNFA_ÎµClosure]
  -- âŠ¢ List.foldl (ÎµNFA.stepSet (toÎµNFA M)) start = List.foldl (stepSet M) start
  suffices ÎµNFA.stepSet (toÎµNFA M) = stepSet M by rw [this]
  -- âŠ¢ ÎµNFA.stepSet (toÎµNFA M) = stepSet M
  ext S s
  -- âŠ¢ xâœ âˆˆ ÎµNFA.stepSet (toÎµNFA M) S s â†” xâœ âˆˆ stepSet M S s
  simp only [stepSet, ÎµNFA.stepSet, exists_prop, Set.mem_iUnion]
  -- âŠ¢ (âˆƒ i, i âˆˆ S âˆ§ xâœ âˆˆ ÎµNFA.ÎµClosure (toÎµNFA M) (ÎµNFA.step (toÎµNFA M) i (some s) â€¦
  apply exists_congr
  -- âŠ¢ âˆ€ (a : Ïƒ), a âˆˆ S âˆ§ xâœ âˆˆ ÎµNFA.ÎµClosure (toÎµNFA M) (ÎµNFA.step (toÎµNFA M) a (so â€¦
  simp only [and_congr_right_iff]
  -- âŠ¢ âˆ€ (a : Ïƒ), a âˆˆ S â†’ (xâœ âˆˆ ÎµNFA.ÎµClosure (toÎµNFA M) (ÎµNFA.step (toÎµNFA M) a (s â€¦
  intro _ _
  -- âŠ¢ xâœ âˆˆ ÎµNFA.ÎµClosure (toÎµNFA M) (ÎµNFA.step (toÎµNFA M) aâœÂ¹ (some s)) â†” xâœ âˆˆ ste â€¦
  rw [M.toÎµNFA_ÎµClosure]
  -- âŠ¢ xâœ âˆˆ ÎµNFA.step (toÎµNFA M) aâœÂ¹ (some s) â†” xâœ âˆˆ step M aâœÂ¹ s
  rfl
  -- ðŸŽ‰ no goals
#align NFA.to_Îµ_NFA_eval_from_match NFA.toÎµNFA_evalFrom_match

@[simp]
theorem toÎµNFA_correct (M : NFA Î± Ïƒ) : M.toÎµNFA.accepts = M.accepts := by
  rw [ÎµNFA.accepts, ÎµNFA.eval, toÎµNFA_evalFrom_match]
  -- âŠ¢ {x | âˆƒ S, S âˆˆ (toÎµNFA M).accept âˆ§ S âˆˆ evalFrom M (toÎµNFA M).start x} = accep â€¦
  rfl
  -- ðŸŽ‰ no goals
#align NFA.to_Îµ_NFA_correct NFA.toÎµNFA_correct

end NFA

/-! ### Regex-like operations -/


namespace ÎµNFA

instance : Zero (ÎµNFA Î± Ïƒ) :=
  âŸ¨âŸ¨fun _ _ â†¦ âˆ…, âˆ…, âˆ…âŸ©âŸ©

instance : One (ÎµNFA Î± Ïƒ) :=
  âŸ¨âŸ¨fun _ _ â†¦ âˆ…, univ, univâŸ©âŸ©

instance : Inhabited (ÎµNFA Î± Ïƒ) :=
  âŸ¨0âŸ©

variable (P : ÎµNFA Î± Ïƒ) (Q : ÎµNFA Î± Ïƒ')

@[simp]
theorem step_zero (s a) : (0 : ÎµNFA Î± Ïƒ).step s a = âˆ… :=
  rfl
#align Îµ_NFA.step_zero ÎµNFA.step_zero

@[simp]
theorem step_one (s a) : (1 : ÎµNFA Î± Ïƒ).step s a = âˆ… :=
  rfl
#align Îµ_NFA.step_one ÎµNFA.step_one

@[simp]
theorem start_zero : (0 : ÎµNFA Î± Ïƒ).start = âˆ… :=
  rfl
#align Îµ_NFA.start_zero ÎµNFA.start_zero

@[simp]
theorem start_one : (1 : ÎµNFA Î± Ïƒ).start = univ :=
  rfl
#align Îµ_NFA.start_one ÎµNFA.start_one

@[simp]
theorem accept_zero : (0 : ÎµNFA Î± Ïƒ).accept = âˆ… :=
  rfl
#align Îµ_NFA.accept_zero ÎµNFA.accept_zero

@[simp]
theorem accept_one : (1 : ÎµNFA Î± Ïƒ).accept = univ :=
  rfl
#align Îµ_NFA.accept_one ÎµNFA.accept_one

end ÎµNFA
