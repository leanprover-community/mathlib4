/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.Algebra.Group.Action.Pi
import Mathlib.CategoryTheory.FintypeCat
import Mathlib.GroupTheory.GroupAction.Quotient
import Mathlib.GroupTheory.QuotientGroup.Basic
import Mathlib.RepresentationTheory.Action.Basic

/-!
# Constructors for `Action V G` for some concrete categories

We construct `Action (Type u) G` from a `[MulAction G X]` instance and give some applications.
-/

universe u v

open CategoryTheory Limits

namespace Action

section

variable {G : Type u} [Monoid G] {A B : Action (Type u) G}

def typeÏ (X : Action (Type u) G) : G â†’* Function.End X.V :=
  X.Ï

@[simp]
theorem typeÏ_mk (X : Type u) (f : G â†’* Function.End X) :
    (Action.mk X f).typeÏ = f :=
  rfl

def hom (f : A âŸ¶ B) : A.V â†’ B.V := f.hom

theorem hom_def (f : A âŸ¶ B) : f.hom = hom f := rfl

theorem types_hom_ext {f g : A âŸ¶ B} (h : hom f = hom g) : f = g :=
  Action.hom_ext _ _ h

@[simp]
theorem comp_hom' (f : A âŸ¶ B) (g : B âŸ¶ A) : hom (f â‰« g) = hom g âˆ˜ hom f := rfl

@[simps]
def mkHom' (f : A.V â†’ B.V) (h : âˆ€ g, f âˆ˜ A.typeÏ g = B.typeÏ g âˆ˜ f) :
    A âŸ¶ B where
  hom := f
  comm g := h g

@[simp]
theorem mkHom'_hom' (f : A.V â†’ B.V) {h : âˆ€ g, f âˆ˜ A.typeÏ g = B.typeÏ g âˆ˜ f} :
    hom (mkHom' f h) = f := rfl

abbrev mkIso' (f : A.V â‰ƒ B.V) (h : âˆ€ g, f âˆ˜ A.typeÏ g = B.typeÏ g âˆ˜ f) :
    A â‰… B :=
  Action.mkIso f.toIso h

@[simp]
theorem mkIso'_hom_hom' (f : A.V â‰ƒ B.V) {h : âˆ€ g, f âˆ˜ A.typeÏ g = B.typeÏ g âˆ˜ f} :
    hom (mkIso' f h).hom = f := rfl

@[simp]
theorem mkIso'_inv_hom' (f : A.V â‰ƒ B.V) {h : âˆ€ g, f âˆ˜ A.typeÏ g = B.typeÏ g âˆ˜ f} :
    hom (mkIso' f h).inv = f.symm := rfl

@[simp]
theorem trivial_typeÏ_apply {X : Type u} (g : G) (x : X) :
    (trivial G X).typeÏ g x = x :=
  rfl

@[simp]
theorem typeÏ_mul_apply {A : Action (Type u) G} (g h : G) (x : A.V) :
    A.typeÏ (g * h) x = A.typeÏ g (A.typeÏ h x) := by
  rw [map_mul]
  rfl

end

section
variable {G : Type u} [Group G] {A : Action (Type u) G}

@[simp]
theorem typeÏ_inv_self_apply (g : G) (x : A.V) :
    A.typeÏ gâ»Â¹ (A.typeÏ g x) = x :=
  show (A.typeÏ gâ»Â¹ * A.typeÏ g) x = x by simp [â† map_mul, Function.End.one_def]

@[simp]
theorem typeÏ_self_inv_apply (g : G) (x : A.V) :
    A.typeÏ g (A.typeÏ gâ»Â¹ x) = x :=
  show (A.typeÏ g * A.typeÏ gâ»Â¹) x = x by simp [â† map_mul, Function.End.one_def]

end

/-- Bundles a type `H` with a multiplicative action of `G` as an `Action`. -/
@[simps (config := .lemmasOnly)]
def ofMulAction (G H : Type u) [Monoid G] [MulAction G H] : Action (Type u) G where
  V := H
  Ï := @MulAction.toEndHom _ _ _ (by assumption)

@[simp]
theorem ofMulAction_apply {G H : Type u} [Monoid G] [MulAction G H] (g : G) (x : H) :
    (ofMulAction G H).typeÏ g x = (g â€¢ x : H) :=
  rfl

/-- Given a family `F` of types with `G`-actions, this is the limit cone demonstrating that the
product of `F` as types is a product in the category of `G`-sets. -/
def ofMulActionLimitCone {Î¹ : Type v} (G : Type max v u) [Monoid G] (F : Î¹ â†’ Type max v u)
    [âˆ€ i : Î¹, MulAction G (F i)] :
    LimitCone (Discrete.functor fun i : Î¹ => Action.ofMulAction G (F i)) where
  cone :=
    { pt := Action.ofMulAction G (âˆ€ i : Î¹, F i)
      Ï€ := Discrete.natTrans (fun i => âŸ¨fun x => x i.as, fun _ => rflâŸ©) }
  isLimit :=
    { lift := fun s =>
        { hom := fun x i => (s.Ï€.app âŸ¨iâŸ©).hom x
          comm := fun g => by
            ext x
            funext j
            exact congr_fun ((s.Ï€.app âŸ¨jâŸ©).comm g) x }
      fac := fun _ _ => rfl
      uniq := fun s f h => by
        ext x
        funext j
        dsimp at *
        rw [â† h âŸ¨jâŸ©]
        rfl }

/-- The `G`-set `G`, acting on itself by left multiplication. -/
abbrev leftRegular (G : Type u) [Monoid G] : Action (Type u) G :=
  Action.ofMulAction G G

/-- The `G`-set `Gâ¿`, acting on itself by left multiplication. -/
abbrev diagonal (G : Type u) [Monoid G] (n : â„•) : Action (Type u) G :=
  Action.ofMulAction G (Fin n â†’ G)

/-- We have `Fin 1 â†’ G â‰… G` as `G`-sets, with `G` acting by left multiplication. -/
def diagonalOneIsoLeftRegular (G : Type u) [Monoid G] : diagonal G 1 â‰… leftRegular G :=
  Action.mkIso (Equiv.funUnique _ _).toIso fun _ => rfl

namespace FintypeCat

/-- If `X` is a type with `[Fintype X]` and `G` acts on `X`, then `G` also acts on
`FintypeCat.of X`. -/
instance (G : Type*) (X : Type*) [Monoid G] [MulAction G X] [Fintype X] :
    MulAction G (FintypeCat.of X) :=
  inferInstanceAs <| MulAction G X

/-- Bundles a finite type `H` with a multiplicative action of `G` as an `Action`. -/
def ofMulAction (G : Type u) (H : FintypeCat.{u}) [Monoid G] [MulAction G H] :
    Action FintypeCat G where
  V := H
  Ï := @MulAction.toEndHom _ _ _ (by assumption)

@[simp]
theorem ofMulAction_apply {G : Type u} {H : FintypeCat.{u}} [Monoid G] [MulAction G H]
    (g : G) (x : H) : (FintypeCat.ofMulAction G H).Ï g x = (g â€¢ x : H) :=
  rfl

section

/-- Shorthand notation for the quotient of `G` by `H` as a finite `G`-set. -/
notation:10 G:10 " â§¸â‚ " H:10 => Action.FintypeCat.ofMulAction G (FintypeCat.of <| G â§¸ H)

variable {G : Type*} [Group G] (H N : Subgroup G) [Fintype (G â§¸ N)]

/-- If `N` is a normal subgroup of `G`, then this is the group homomorphism
sending an element `g` of `G` to the `G`-endomorphism of `G â§¸â‚ N` given by
multiplication with `gâ»Â¹` on the right. -/
def toEndHom [N.Normal] : G â†’* End (G â§¸â‚ N) where
  toFun v := {
    hom := Quotient.lift (fun Ïƒ â†¦ âŸ¦Ïƒ * vâ»Â¹âŸ§) <| fun a b h â†¦ Quotient.sound <| by
      apply (QuotientGroup.leftRel_apply).mpr
      simp only [mul_inv_rev, inv_inv]
      convert_to v * (aâ»Â¹ * b) * vâ»Â¹ âˆˆ N
      Â· group
      Â· exact Subgroup.Normal.conj_mem â€¹_â€º _ (QuotientGroup.leftRel_apply.mp h) _
    comm := fun (g : G) â†¦ by
      ext (x : G â§¸ N)
      induction' x using Quotient.inductionOn with x
      simp only [FintypeCat.comp_apply, Action.FintypeCat.ofMulAction_apply, Quotient.lift_mk]
      show Quotient.lift (fun Ïƒ â†¦ âŸ¦Ïƒ * vâ»Â¹âŸ§) _ (âŸ¦g â€¢ xâŸ§) = _
      simp only [smul_eq_mul, Quotient.lift_mk, mul_assoc]
      rfl
  }
  map_one' := by
    apply Action.hom_ext
    ext (x : G â§¸ N)
    induction' x using Quotient.inductionOn with x
    simp
  map_mul' Ïƒ Ï„ := by
    apply Action.hom_ext
    ext (x : G â§¸ N)
    induction' x using Quotient.inductionOn with x
    show âŸ¦x * (Ïƒ * Ï„)â»Â¹âŸ§ = âŸ¦x * Ï„â»Â¹ * Ïƒâ»Â¹âŸ§
    rw [mul_inv_rev, mul_assoc]

@[simp]
lemma toEndHom_apply [N.Normal] (g h : G) : (toEndHom N g).hom âŸ¦hâŸ§ = âŸ¦h * gâ»Â¹âŸ§ := rfl

variable {N} in
lemma toEndHom_trivial_of_mem [N.Normal] {n : G} (hn : n âˆˆ N) : toEndHom N n = ðŸ™ (G â§¸â‚ N) := by
  apply Action.hom_ext
  ext (x : G â§¸ N)
  induction' x using Quotient.inductionOn with Î¼
  exact Quotient.sound ((QuotientGroup.leftRel_apply).mpr <| by simpa)

/-- If `H` and `N` are subgroups of a group `G` with `N` normal, there is a canonical
group homomorphism `H â§¸ N âŠ“ H` to the `G`-endomorphisms of `G â§¸ N`. -/
def quotientToEndHom [N.Normal] : H â§¸ Subgroup.subgroupOf N H â†’* End (G â§¸â‚ N) :=
  QuotientGroup.lift (Subgroup.subgroupOf N H) ((toEndHom N).comp H.subtype) <| fun _ uinU' â†¦
    toEndHom_trivial_of_mem uinU'

@[simp]
lemma quotientToEndHom_mk [N.Normal] (x : H) (g : G) :
    (quotientToEndHom H N âŸ¦xâŸ§).hom âŸ¦gâŸ§ = âŸ¦g * xâ»Â¹âŸ§ :=
  rfl

/-- If `N` and `H` are subgroups of a group `G` with `N â‰¤ H`, this is the canonical
`G`-morphism `G â§¸ N âŸ¶ G â§¸ H`. -/
def quotientToQuotientOfLE [Fintype (G â§¸ H)] (h : N â‰¤ H) : (G â§¸â‚ N) âŸ¶ (G â§¸â‚ H) where
  hom := Quotient.lift _ <| fun _ _ hab â†¦ Quotient.sound <|
    (QuotientGroup.leftRel_apply).mpr (h <| (QuotientGroup.leftRel_apply).mp hab)
  comm g := by
    ext (x : G â§¸ N)
    induction' x using Quotient.inductionOn with Î¼
    rfl

@[simp]
lemma quotientToQuotientOfLE_hom_mk [Fintype (G â§¸ H)] (h : N â‰¤ H) (x : G) :
    (quotientToQuotientOfLE H N h).hom âŸ¦xâŸ§ = âŸ¦xâŸ§ :=
  rfl

end

end FintypeCat

section ToMulAction

variable {V : Type (u + 1)} [LargeCategory V] [ConcreteCategory V]

instance instMulAction {G : Type u} [Monoid G] (X : Action V G) :
    MulAction G ((CategoryTheory.forget _).obj X) where
  smul g x := ((CategoryTheory.forget _).map (X.Ï g)) x
  one_smul x := by
    show ((CategoryTheory.forget _).map (X.Ï 1)) x = x
    simp only [Action.Ï_one, FunctorToTypes.map_id_apply]
  mul_smul g h x := by
    show (CategoryTheory.forget V).map (X.Ï (g * h)) x =
      ((CategoryTheory.forget V).map (X.Ï h) â‰« (CategoryTheory.forget V).map (X.Ï g)) x
    rw [â† Functor.map_comp, map_mul]
    rfl

/- Specialize `instMulAction` to assist typeclass inference. -/
instance {G : Type u} [Monoid G] (X : Action FintypeCat G) : MulAction G X.V :=
  Action.instMulAction X
instance {G : Type u} [Group G] (X : Action FintypeCat G) : MulAction G X.V :=
  Action.instMulAction X

end ToMulAction

end Action
