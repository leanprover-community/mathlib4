/-
Copyright (c) 2025 Amelia Livingston. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Amelia Livingston
-/
import Mathlib.RepresentationTheory.Rep

/-!
# Coinvariants a group representation

Given a commutative ring `k` and a monoid `G`, this file introduces the coinvariants of a
`k`-linear `G`-representation `(V, Ï)`.

We first define `Representation.augmentationSubmodule`, the submodule of `V` generated by elements
of the form `Ï g x - x` for `x : V`, `g : G`. Then the coinvariants of `(V, Ï)` are the quotient of
`V` by this submodule.

## Main definitions

* `Representation.coinvariants Ï`: the coinvariants of a representation `Ï`.
* `Representation.coinvariantsFinsuppLEquiv Ï Î±`: given a type `Î±`, this is the `k`-linear
equivalence between `(Î± â†’â‚€ V)_G` and `Î± â†’â‚€ V_G`.
* `Representation.coinvariantsTprodLeftRegularLEquiv Ï`: the `k`-linear equivalence between
`(V âŠ— k[G])_G` and `V` sending `âŸ¦v âŠ— single g râŸ§ â†¦ r â€¢ Ï(gâ»Â¹)(v)`.
* `Rep.coinvariantsAdjunction k G`: the adjunction between the functor sending a representation to
its coinvariants and the functor equipping a module with the trivial representation.
* `Rep.coinvariantsTensor k G`: the functor sending representations `A, B` to `(A âŠ—[k] B)_G`. This
is naturally isomorphic to the functor sending `A, B` to `A âŠ—[k[G]] B`, where we give `A` the
`k[G]áµáµ’áµ–`-module structure defined by `g â€¢ a := A.Ï gâ»Â¹ a`.
* `Rep.coinvariantsTensorFreeLEquiv A Î±`: given a representation `A` and a type `Î±`, this is the
`k`-linear equivalence between `(A âŠ— (Î± â†’â‚€ k[G]))_G` and `Î± â†’â‚€ A` sending
`âŸ¦a âŠ— single x (single g r)âŸ§ â†¦ single x (r â€¢ Ï(gâ»Â¹)(a))`. This is useful for group homology.

-/

universe u v

namespace Representation

section

variable {k G V W : Type*} [CommRing k] [Monoid G] [AddCommGroup V] [Module k V]
  [AddCommGroup W] [Module k W] (Ï : Representation k G V) (Ï„ : Representation k G W)

/-- The submodule of a representation generated by elements of the form `Ï g x - x`. -/
def augmentationSubmodule : Submodule k V :=
  Submodule.span k (Set.range fun (x : G Ã— V) => Ï x.1 x.2 - x.2)

variable {Ï}

lemma mem_augmentationSubmodule_of_eq (g : G) (x : V) (a : V) (h : Ï g x - x = a) :
    a âˆˆ augmentationSubmodule Ï :=
  Submodule.subset_span âŸ¨(g, x), hâŸ©

variable (Ï)

@[simp]
theorem augmentationSubmodule_eq_bot_of_isTrivial [Ï.IsTrivial] :
    augmentationSubmodule Ï = âŠ¥ := by
  rw [augmentationSubmodule, Submodule.span_eq_bot]
  rintro x âŸ¨âŸ¨g, yâŸ©, rflâŸ©
  simp

/-- The coinvariants of a representation, `V â§¸ âŸ¨{Ï g x - x | g âˆˆ G, x âˆˆ V}âŸ©`. -/
def coinvariants := V â§¸ augmentationSubmodule Ï

instance : AddCommGroup (coinvariants Ï) := by unfold coinvariants; infer_instance

instance : Module k (coinvariants Ï) := by unfold coinvariants; infer_instance

/-- The quotient map from a representation to its coinvariants as a linear map. -/
def coinvariantsMk : V â†’â‚—[k] coinvariants Ï := Submodule.mkQ (augmentationSubmodule Ï)

theorem coinvariantsMk_eq_iff {x y : V} :
    coinvariantsMk Ï x = coinvariantsMk Ï y â†” x - y âˆˆ Ï.augmentationSubmodule :=
  Submodule.Quotient.eq _

@[simp]
lemma coinvariantsMk_Ï_apply (g : G) (x : V) :
    coinvariantsMk Ï (Ï g x) = coinvariantsMk Ï x :=
  (coinvariantsMk_eq_iff _).2 <| mem_augmentationSubmodule_of_eq g x _ rfl

@[elab_as_elim]
theorem coinvariants.induction_on {C : coinvariants Ï â†’ Prop} (x : coinvariants Ï)
    (h : âˆ€ v : V, C (coinvariantsMk Ï v)) : C x :=
  Submodule.Quotient.induction_on _ x h

/-- A `G`-invariant linear map induces a linear map out of the coinvariants of a
`G`-representation. -/
def coinvariantsLift (f : V â†’â‚—[k] W) (h : âˆ€ (x : G), f âˆ˜â‚— Ï x = f) :
    Ï.coinvariants â†’â‚—[k] W :=
  Submodule.liftQ _ f <| Submodule.span_le.2 fun x âŸ¨âŸ¨g, yâŸ©, hyâŸ© => by
    simpa only [â† hy, SetLike.mem_coe, LinearMap.mem_ker, map_sub, sub_eq_zero, LinearMap.coe_comp,
      Function.comp_apply] using LinearMap.ext_iff.1 (h g) y

variable {Ï}

@[simp]
theorem coinvariantsLift_comp_mk (f : V â†’â‚—[k] W) (h : âˆ€ (x : G), f âˆ˜â‚— Ï x = f) :
  coinvariantsLift Ï f h âˆ˜â‚— coinvariantsMk Ï = f := rfl

@[simp]
theorem coinvariantsLift_mk (f : V â†’â‚—[k] W) (h : âˆ€ (x : G), f âˆ˜â‚— Ï x = f) (x : V) :
  coinvariantsLift Ï f h (coinvariantsMk _ x) = f x := rfl

@[ext]
lemma coinvariants_hom_ext {f g : coinvariants Ï â†’â‚—[k] W}
    (H : f âˆ˜â‚— coinvariantsMk Ï = g âˆ˜â‚— coinvariantsMk Ï) : f = g :=
  Submodule.linearMap_qext _ H

variable (Ï)

noncomputable def coinvariantsMap (f : V â†’â‚—[k] W) (hf : âˆ€ g, f âˆ˜â‚— Ï g = Ï„ g âˆ˜â‚— f) :
    coinvariants Ï â†’â‚—[k] coinvariants Ï„ :=
  coinvariantsLift _ (coinvariantsMk _ âˆ˜â‚— f) fun g => LinearMap.ext fun x =>
    (coinvariantsMk_eq_iff _).2 <| mem_augmentationSubmodule_of_eq (Ï := Ï„) g (f x) _ <| by
      simpa using congr($((hf g).symm) x)

variable {Ï Ï„}

@[simp]
lemma coinvariantsMap_comp_mk {f : V â†’â‚—[k] W} {hf : âˆ€ g, f âˆ˜â‚— Ï g = Ï„ g âˆ˜â‚— f} :
    coinvariantsMap Ï Ï„ f hf âˆ˜â‚— coinvariantsMk Ï = coinvariantsMk Ï„ âˆ˜â‚— f := rfl

@[simp]
lemma coinvariantsMap_mk (f : V â†’â‚—[k] W) (hf : âˆ€ g, f âˆ˜â‚— Ï g = Ï„ g âˆ˜â‚— f) (x : V) :
    coinvariantsMap Ï Ï„ f hf (coinvariantsMk _ x) = coinvariantsMk _ (f x) := rfl

end
section Finsupp

open Finsupp

variable {k G V : Type*} [CommRing k] [Group G] [AddCommGroup V] [Module k V]
  (Ï : Representation k G V) (Î± : Type*)

/-- Given a `G`-representation `(V, Ï)` and a type `Î±`, this is the map `(Î± â†’â‚€ V)_G â†’â‚— (Î± â†’â‚€ V_G)`
sending `âŸ¦single a vâŸ§ â†¦ single a âŸ¦vâŸ§`. -/
noncomputable def coinvariantsToFinsupp :
    coinvariants (Ï.finsupp Î±) â†’â‚—[k] Î± â†’â‚€ coinvariants Ï :=
  coinvariantsLift _ (mapRange.linearMap (coinvariantsMk _)) <| fun g => by ext; simp

variable {Ï Î±}

@[simp]
lemma coinvariantsToFinsupp_mk_single (x : Î±) (a : V) :
    coinvariantsToFinsupp Ï Î± (coinvariantsMk _ (single x a)) =
      single x (coinvariantsMk _ a) := by
  simp [coinvariantsToFinsupp]

variable (Ï Î±) in
/-- Given a `G`-representation `(V, Ï)` and a type `Î±`, this is the map `(Î± â†’â‚€ V_G) â†’â‚— (Î± â†’â‚€ V)_G`
sending `single a âŸ¦vâŸ§ â†¦ âŸ¦single a vâŸ§`. -/
noncomputable def finsuppToCoinvariants :
    (Î± â†’â‚€ coinvariants Ï) â†’â‚—[k] coinvariants (Ï.finsupp Î±) :=
  lsum (R := k) k fun a => coinvariantsLift _ (coinvariantsMk _ âˆ˜â‚— lsingle a) fun g =>
    LinearMap.ext fun x => (coinvariantsMk_eq_iff _).2 <|
    mem_augmentationSubmodule_of_eq g (single a x) _ <| by simp

@[simp]
lemma finsuppToCoinvariants_single_mk (a : Î±) (x : V) :
    finsuppToCoinvariants Ï Î± (single a <| coinvariantsMk _ x) =
      coinvariantsMk _ (single a x) := by
  simp [finsuppToCoinvariants]

variable (Ï Î±) in
/-- Given a `G`-representation `(V, Ï)` and a type `Î±`, this is the linear equivalence
`(Î± â†’â‚€ V)_G â‰ƒâ‚— (Î± â†’â‚€ V_G)` sending `âŸ¦single a vâŸ§ â†¦ single a âŸ¦vâŸ§`. -/
@[simps! symm_apply]
noncomputable def coinvariantsFinsuppLEquiv :
    coinvariants (Ï.finsupp Î±) â‰ƒâ‚—[k] Î± â†’â‚€ coinvariants Ï :=
  LinearEquiv.ofLinear (coinvariantsToFinsupp Ï Î±) (finsuppToCoinvariants Ï Î±)
    (by ext; simp) (by ext; simp)

@[simp]
lemma coinvariantsFinsuppLEquiv_apply (x) :
    coinvariantsFinsuppLEquiv Ï Î± x = coinvariantsToFinsupp Ï Î± x := by rfl

end Finsupp

section TensorProduct

open TensorProduct

variable {k G V W : Type*} [CommRing k] [Group G] [AddCommGroup V] [Module k V]
  [AddCommGroup W] [Module k W] (Ï : Representation k G V) (Ï„ : Representation k G W)

@[simp]
lemma coinvariantsMk_inv_tmul (x : V) (y : W) (g : G) :
    coinvariantsMk (Ï.tprod Ï„) (Ï gâ»Â¹ x âŠ—â‚œ[k] y) = coinvariantsMk (Ï.tprod Ï„) (x âŠ—â‚œ[k] Ï„ g y) :=
  (coinvariantsMk_eq_iff _).2 <| mem_augmentationSubmodule_of_eq gâ»Â¹ (x âŠ—â‚œ[k] Ï„ g y) _ <| by simp

@[simp]
lemma coinvariantsMk_tmul_inv (x : V) (y : W) (g : G) :
    coinvariantsMk (Ï.tprod Ï„) (x âŠ—â‚œ[k] Ï„ gâ»Â¹ y) = coinvariantsMk (Ï.tprod Ï„) (Ï g x âŠ—â‚œ[k] y) :=
  (coinvariantsMk_eq_iff _).2 <| mem_augmentationSubmodule_of_eq gâ»Â¹ (Ï g x âŠ—â‚œ[k] y) _ <| by simp

/-- Given a `k`-linear `G`-representation `V, Ï`, this is the map `(V âŠ— k[G])_G â†’â‚—[k] V` sending
`âŸ¦v âŠ— single g râŸ§ â†¦ r â€¢ Ï(gâ»Â¹)(v)`. -/
noncomputable def ofCoinvariantsTprodLeftRegular :
    coinvariants (Ï.tprod (leftRegular k G)) â†’â‚—[k] V :=
  coinvariantsLift _ (TensorProduct.lift (Finsupp.linearCombination _ fun g => Ï gâ»Â¹) âˆ˜â‚—
    (TensorProduct.comm _ _ _).toLinearMap) fun _ => by ext; simp

@[simp]
lemma ofCoinvariantsTprodLeftRegular_mk_tmul_single (x : V) (g : G) (r : k) :
    ofCoinvariantsTprodLeftRegular Ï (coinvariantsMk _ (x âŠ—â‚œ Finsupp.single g r)) = r â€¢ Ï gâ»Â¹ x :=
  congr($(Finsupp.linearCombination_single k (v := fun g => Ï gâ»Â¹) r g) x)

/-- Given a `k`-linear `G`-representation `(V, Ï)`, this is the linear equivalence
`(V âŠ— k[G])_G â‰ƒâ‚—[k] V` sending `âŸ¦v âŠ— single g râŸ§ â†¦ r â€¢ Ï(gâ»Â¹)(v)`. -/
@[simps! symm_apply]
noncomputable def coinvariantsTprodLeftRegularLEquiv :
    coinvariants (Ï.tprod (leftRegular k G)) â‰ƒâ‚—[k] V :=
  LinearEquiv.ofLinear (ofCoinvariantsTprodLeftRegular Ï)
    (coinvariantsMk _ âˆ˜â‚— (mk k V (G â†’â‚€ k)).flip (Finsupp.single 1 1)) (by ext; simp) <| by
      ext x g
      exact (coinvariantsMk_eq_iff _).2 <|
      mem_augmentationSubmodule_of_eq gâ»Â¹ (x âŠ—â‚œ Finsupp.single g 1) _ <| by
        simp [smul_tmul', smul_tmul]

@[simp]
lemma coinvariantsTprodLeftRegularLEquiv_apply (x) :
    coinvariantsTprodLeftRegularLEquiv Ï x = ofCoinvariantsTprodLeftRegular Ï x := by
  rfl

end TensorProduct

end Representation

namespace Rep

open CategoryTheory

section

variable {k G : Type u} [CommRing k] [Monoid G] {A B C : Rep k G} {n : â„•}

open Representation

/-- The linear map underlying a `G`-representation morphism `A âŸ¶ B`, where `B` has the trivial
representation, factors through `A_G`. -/
noncomputable abbrev coinvariantsLift [B.Ï.IsTrivial] (f : A âŸ¶ B) :
    coinvariants A.Ï â†’â‚—[k] B :=
  Representation.coinvariantsLift _ f.hom.hom fun _ => by
    ext
    have := hom_comm_apply f
    simp_all

/-- A `G`-representation morphism `A âŸ¶ B` induces a linear map `A_G â†’â‚—[k] B_G`. -/
noncomputable abbrev coinvariantsMap (f : A âŸ¶ B) :
    coinvariants A.Ï â†’â‚—[k] coinvariants B.Ï :=
  Representation.coinvariantsMap _ _ f.hom.hom fun _ => ModuleCat.hom_ext_iff.1 (f.comm _)

@[simp]
theorem coinvariantsMap_id (A : Rep k G) :
    coinvariantsMap (ğŸ™ A) = LinearMap.id := by
  ext; rfl

@[simp]
theorem coinvariantsMap_comp (f : A âŸ¶ B) (g : B âŸ¶ C) :
    coinvariantsMap (f â‰« g) = coinvariantsMap g âˆ˜â‚— coinvariantsMap f := by
  ext; rfl

variable (k G)

/-- The functor sending a representation to its coinvariants. -/
@[simps obj map]
noncomputable def coinvariantsFunctor : Rep k G â¥¤ ModuleCat k where
  obj A := ModuleCat.of k (A.Ï.coinvariants)
  map f := ModuleCat.ofHom (coinvariantsMap f)

variable {k G} in
@[ext]
lemma coinvariantsFunctor_hom_ext {M : ModuleCat k} {f g : (coinvariantsFunctor k G).obj A âŸ¶ M}
    (hfg : f.hom âˆ˜â‚— coinvariantsMk _ = g.hom âˆ˜â‚— coinvariantsMk _) :
    f = g := ModuleCat.hom_ext <| coinvariants_hom_ext hfg

instance : (coinvariantsFunctor k G).Additive where
  map_add := by intros; ext; rfl

/-- The adjunction between the functor sending a representation to its coinvariants and the functor
equipping a module with the trivial representation. -/
@[simps! unit_app_hom_hom counit_app_hom]
noncomputable def coinvariantsAdjunction : coinvariantsFunctor k G âŠ£ trivialFunctor G :=
  Adjunction.mkOfHomEquiv {
    homEquiv X Y := {
      toFun f := {
        hom := ModuleCat.ofHom (f.hom âˆ˜â‚— X.Ï.coinvariantsMk)
        comm g := by
          ext x
          exact congr(f.hom $((coinvariantsMk_eq_iff _).2
            (X.Ï.mem_augmentationSubmodule_of_eq g x _ rfl))) }
      invFun f := ModuleCat.ofHom (coinvariantsLift f)
      left_inv _ := by ext; rfl
      right_inv _ := rfl }
    homEquiv_naturality_left_symm _ _ := by ext; rfl
    homEquiv_naturality_right := by intros; rfl }

instance : (coinvariantsFunctor k G).PreservesZeroMorphisms where
  map_zero _ _ := by ext; rfl

instance : Limits.PreservesColimits (coinvariantsFunctor k G) :=
  (coinvariantsAdjunction k G).leftAdjoint_preservesColimits

open MonoidalCategory ModuleCat.MonoidalCategory in
/-- The functor sending `A, B` to `(A âŠ—[k] B)_G`. This is naturally isomorphic to the functor
sending `A, B` to `A âŠ—[k[G]] B`, where we give `A` the `k[G]áµáµ’áµ–`-module structure defined by
`g â€¢ a := A.Ï gâ»Â¹ a`. -/
@[simps]
noncomputable def coinvariantsTensor : Rep k G â¥¤ Rep k G â¥¤ ModuleCat k where
  obj A := MonoidalCategory.tensorLeft A â‹™ coinvariantsFunctor k G
  map f := {
    app A := ModuleCat.ofHom (coinvariantsMap (f âŠ— ğŸ™ A))
    naturality _ _ _ := coinvariantsFunctor_hom_ext <| TensorProduct.ext' fun _ _ => by rfl }
  map_id _ := NatTrans.ext <| funext fun _ => by simp
  map_comp _ _ := NatTrans.ext <| funext fun _ => by simp

variable {k G} (A B)

noncomputable abbrev coinvariantsTensorMk :
    A â†’â‚—[k] B â†’â‚—[k] ((coinvariantsTensor k G).obj A).obj B :=
  (TensorProduct.mk k A B).comprâ‚‚ (coinvariantsMk _)

variable {A B}

lemma coinvariantsTensorMk_apply (a : A) (b : B) :
  coinvariantsTensorMk A B a b = coinvariantsMk _ (a âŠ—â‚œ[k] b) := rfl

@[ext]
lemma coinvariantsTensor_hom_ext {M : ModuleCat k}
    {f g : ((coinvariantsTensor k G).obj A).obj B âŸ¶ M}
    (hfg : (coinvariantsTensorMk A B).comprâ‚‚ f.hom = (coinvariantsTensorMk A B).comprâ‚‚ g.hom) :
    f = g := coinvariantsFunctor_hom_ext <| TensorProduct.ext <| hfg

instance (A : Rep k G) : ((coinvariantsTensor k G).obj A).Additive := by
  unfold coinvariantsTensor
  infer_instance

end

section Finsupp

variable {k G : Type u} [CommRing k] [Group G] (A : Rep k G) (Î± : Type u) [DecidableEq Î±]

open MonoidalCategory Finsupp Representation

/-- Given a `k`-linear `G`-representation `(A, Ï)` and a type `Î±`, this is the map
`(A âŠ— (Î± â†’â‚€ k[G]))_G â†’â‚—[k] (Î± â†’â‚€ A)` sending
`âŸ¦a âŠ— single x (single g r)âŸ§ â†¦ single x (r â€¢ Ï(gâ»Â¹)(a)).` -/
noncomputable def coinvariantsTensorFreeToFinsupp :
    (A âŠ— free k G Î±).Ï.coinvariants â†’â‚—[k] (Î± â†’â‚€ A) :=
  (coinvariantsFinsuppLEquiv _ Î± â‰ªâ‰«â‚— lcongr (Equiv.refl Î±)
    (coinvariantsTprodLeftRegularLEquiv A.Ï)).toLinearMap âˆ˜â‚— coinvariantsMap (finsuppTensorRight A
      (leftRegular k G) Î±).hom

variable {A Î±}

@[simp]
lemma coinvariantsTensorFreeToFinsupp_mk_tmul_single (x : A) (i : Î±) (g : G) (r : k) :
    DFunLike.coe (F := (A.Ï.tprod (Representation.free k G Î±)).coinvariants â†’â‚—[k] Î± â†’â‚€ A.V)
      (coinvariantsTensorFreeToFinsupp A Î±) (coinvariantsMk _ (x âŠ—â‚œ single i (single g r))) =
      single i (r â€¢ A.Ï gâ»Â¹ x) := by
  simp [ModuleCat.MonoidalCategory.instMonoidalCategoryStruct_tensorObj,
    ModuleCat.MonoidalCategory.tensorObj, coinvariantsTensorFreeToFinsupp,
    coinvariantsMap, finsuppTensorRight, TensorProduct.finsuppRight]

variable (A Î±)

/-- Given a `k`-linear `G`-representation `(A, Ï)` and a type `Î±`, this is the map
`(Î± â†’â‚€ A) â†’â‚—[k] (A âŠ— (Î± â†’â‚€ k[G]))_G` sending `single x a â†¦ âŸ¦a âŠ—â‚œ single x 1âŸ§.` -/
noncomputable def finsuppToCoinvariantsTensorFree :
    (Î± â†’â‚€ A) â†’â‚—[k] coinvariants (A âŠ— (free k G Î±)).Ï :=
  coinvariantsMap ((finsuppTensorRight A (leftRegular k G) Î±)).inv âˆ˜â‚—
    (coinvariantsFinsuppLEquiv _ Î± â‰ªâ‰«â‚—
    lcongr (Equiv.refl Î±) (coinvariantsTprodLeftRegularLEquiv A.Ï)).symm.toLinearMap

variable {A Î±}

@[simp]
lemma finsuppToCoinvariantsTensorFree_single (i : Î±) (x : A) :
    DFunLike.coe (F := (Î± â†’â‚€ A.V) â†’â‚—[k] (A.Ï.tprod (Representation.free k G Î±)).coinvariants)
      (finsuppToCoinvariantsTensorFree A Î±) (single i x) =
      coinvariantsMk _ (x âŠ—â‚œ single i (single (1 : G) (1 : k))) := by
  simp [finsuppToCoinvariantsTensorFree, coinvariantsMap, ModuleCat.MonoidalCategory.tensorObj,
    ModuleCat.MonoidalCategory.instMonoidalCategoryStruct_tensorObj]

variable (A Î±)

/-- Given a `k`-linear `G`-representation `(A, Ï)` and a type `Î±`, this is the linear equivalence
`(A âŠ— (Î± â†’â‚€ k[G]))_G â‰ƒâ‚—[k] (Î± â†’â‚€ A)` sending
`âŸ¦a âŠ— single x (single g r)âŸ§ â†¦ single x (r â€¢ Ï(gâ»Â¹)(a)).` -/
@[simps! symm_apply]
noncomputable abbrev coinvariantsTensorFreeLEquiv :
    coinvariants (A âŠ— free k G Î±).Ï â‰ƒâ‚—[k] (Î± â†’â‚€ A) :=
  LinearEquiv.ofLinear (coinvariantsTensorFreeToFinsupp A Î±) (finsuppToCoinvariantsTensorFree A Î±)
    (lhom_ext fun i x => by
      simp [finsuppToCoinvariantsTensorFree_single i x,
        coinvariantsTensorFreeToFinsupp_mk_tmul_single x i 1 1]) <|
    coinvariants_hom_ext <| TensorProduct.ext <| LinearMap.ext fun a => lhom_ext' fun i =>
      lhom_ext fun g r => by
        simp [coinvariantsTensorFreeToFinsupp_mk_tmul_single a i g r,
          finsuppToCoinvariantsTensorFree_single (A := A) i, TensorProduct.smul_tmul]

@[simp]
lemma coinvariantsTensorFreeLEquiv_apply (x) :
    DFunLike.coe (F := (A.Ï.tprod (Representation.free k G Î±)).coinvariants â†’â‚—[k] Î± â†’â‚€ A)
      (A.coinvariantsTensorFreeToFinsupp Î±) x = coinvariantsTensorFreeToFinsupp A Î± x := by
  rfl

end Finsupp
end Rep
