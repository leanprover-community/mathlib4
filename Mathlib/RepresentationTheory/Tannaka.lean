/-
Copyright (c) 2025 Yacine Benmeuraiem. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yacine Benmeuraiem
-/
import Mathlib.RepresentationTheory.FDRep

/-!
# Tannaka duality for finite groups

In this file we prove Tannaka duality for finite groups.

The theorem can be formulated as follows: for any field `k`, a finite group `G` can be recovered
from `FDRep k G`, the monoidal category of finite dimensional `k`-linear representations of `G`,
and the monoidal forgetful functor `F : FDRep k G â¥¤ FGModuleCat k`.

## Notation

- `F`   : the monoidal forgetful functor `FDRep k G â¥¤ FGModuleCat k`
- `T`   : the morphism `G â†’* Aut (F k G)` shown to be an isomorphism
- `Ï„áµ£`  : the right regular representation on `G â†’ k`
- `Î±`   : the map sending a monoidal natural isomorphism `Î· : Aut (F k G)` to its `Ï„áµ£` component

## Reference

<https://math.leidenuniv.nl/scripties/1bachCommelin.pdf>
-/

noncomputable section

open CategoryTheory MonoidalCategory ModuleCat Finset Pi

universe u

namespace TannakaDuality

variable {k G : Type u} [Field k] [Group G]

section definitions

instance : (forgetâ‚‚ (FDRep k G) (FGModuleCat k)).Monoidal := by
  change (Action.forget _ _).Monoidal; infer_instance

variable (k G) in
/-- The monoidal forgetful functor from `FDRep k G` to `FGModuleCat k` -/
def F := LaxMonoidalFunctor.of (forgetâ‚‚ (FDRep k G) (FGModuleCat k))

/-- Definition of `T g : Aut (F k G)` by its components -/
def T_app (g : G) (X : FDRep k G) : X.V â‰… X.V where
  hom := ofHom (X.Ï g)
  inv := ofHom (X.Ï gâ»Â¹)
  hom_inv_id := by
    ext x
    change (X.Ï gâ»Â¹ * X.Ï g) x = x
    simp [â† map_mul]
  inv_hom_id := by
    ext x
    change (X.Ï g * X.Ï gâ»Â¹) x = x
    simp [â† map_mul]

/-- The function defining `T` -/
def T_fun (g : G) : Aut (F k G) :=
  LaxMonoidalFunctor.isoOfComponents (T_app g) (fun f â†¦ (f.comm g).symm) rfl (by intros; rfl)

@[simp]
lemma T_apply (g : G) (X : FDRep k G) : ((T_fun g).hom.hom.app X).hom = X.Ï g := rfl

variable (k G) in
/-- The group homomorphism `G â†’* Aut (F k G)` shown to be bijective -/
def T : G â†’* Aut (F k G) where
  toFun := T_fun
  map_one' := by ext; simp; rfl
  map_mul' _ _ := by ext; simp; rfl

/-- The representation on `G â†’ k` induced by multiplication on the right in `G` -/
@[simps]
def Ï„áµ£ : Representation k G (G â†’ k) where
  toFun s := {
    toFun f t := f (t * s)
    map_add' _ _ := rfl
    map_smul' _ _ := rfl
  }
  map_one' := by
    ext
    simp
  map_mul' _ _ := by
    ext
    simp [mul_assoc]

/-- The representation on `G â†’ k` induced by multiplication on the left in `G` -/
@[simps]
def Ï„â‚— : Representation k G (G â†’ k) where
  toFun s := {
    toFun f t := f (sâ»Â¹ * t)
    map_add' _ _ := rfl
    map_smul' _ _ := rfl
  }
  map_one' := by
    ext
    simp
  map_mul' _ _ := by
    ext
    simp [mul_assoc]

variable [Fintype G]

variable (k G) in
/-- The right regular representation `Ï„áµ£` on `G â†’ k` as a `FDRep k G` -/
def fdRepÏ„áµ£ : FDRep k G := FDRep.of Ï„áµ£

/-- Map sending `Î· : Aut (F k G)` to its component at `fdRepÏ„áµ£ k G` as a linear map -/
def Î± (Î· : Aut (F k G)) : (G â†’ k) â†’â‚—[k] (G â†’ k) := (Î·.hom.hom.app (fdRepÏ„áµ£ k G)).hom

end definitions

variable [Fintype G]

lemma T_injective [DecidableEq G] : Function.Injective (T k G) := by
  rw [injective_iff_map_eq_one]
  intro s h
  apply_fun Î± at h
  apply_fun (Â· (single 1 1) 1) at h
  change (single 1 1 : G â†’ k) (1 * s) = (single 1 1 : G â†’ k) 1 at h
  simp_all [single_apply]

/-- An algebra morphism `Ï† : (G â†’ k) â†’â‚[k] k` is an evaluation map. -/
lemma eval_of_alghom {G : Type u} [DecidableEq G] [Fintype G] (Ï† : (G â†’ k) â†’â‚[k] k) :
    âˆƒ (s : G), Ï† = evalAlgHom _ _ s := by
  have h1 := map_one Ï†
  rw [â† univ_sum_single (1 : G â†’ k), map_sum] at h1
  obtain âŸ¨s, hsâŸ© : âˆƒ (s : G), Ï† (single s 1) â‰  0 := by
    by_contra
    simp_all
  have h2 : âˆ€ t â‰  s, Ï† (single t 1) = 0 := by
    intros
    apply eq_zero_of_ne_zero_of_mul_right_eq_zero hs
    rw [â† map_mul]
    convert map_zero Ï†
    ext u
    by_cases u = s <;> simp_all
  have h3 : Ï† (single s 1) = 1 := by
    rwa [Fintype.sum_eq_single s] at h1
    exact h2
  use s
  apply AlgHom.toLinearMap_injective
  apply Basis.ext (basisFun k G)
  intro t
  by_cases t = s <;> simp_all

/-- The `FDRep k G` morphism induced by multiplication on `G â†’ k`. -/
def Î¼ : fdRepÏ„áµ£ k G âŠ— fdRepÏ„áµ£ k G âŸ¶ fdRepÏ„áµ£ k G where
  hom := ofHom (LinearMap.mul' k (G â†’ k))
  comm := by
    intro
    ext (u : TensorProduct k (G â†’ k) (G â†’ k))
    refine TensorProduct.induction_on u rfl (fun _ _ â†¦ rfl) ?_
    intro _ _ hx hy
    simp only [map_add, hx, hy]

/-- For `Î· : Aut (F k G)`, `Î± Î·` is compatible with multiplication -/
lemma map_mul_Î± (Î· : Aut (F k G)) : âˆ€ (x y : G â†’ k), (Î± Î·) (x * y) = ((Î± Î·) x) * ((Î± Î·) y) := by
  intro f g
  have nat := Î·.hom.hom.naturality Î¼
  have tensor := Î·.hom.isMonoidal.tensor
  have F_Î¼ {X Y} : Functor.LaxMonoidal.Î¼ (F k G).toFunctor X Y = ğŸ™ _ := rfl
  simp only [F_Î¼, Category.id_comp, Category.comp_id] at tensor
  rw [tensor] at nat
  apply_fun Hom.hom at nat
  apply_fun (Â· (f âŠ—â‚œ[k] g)) at nat
  exact nat

/-- For `Î· : Aut (F k G)`, `Î± Î·` gives rise to an algebra morphism `(G â†’ k) â†’â‚[k] (G â†’ k)` -/
def algHomOfÎ± (Î· : Aut (F k G)) : (G â†’ k) â†’â‚[k] (G â†’ k) := by
  refine AlgHom.ofLinearMap (Î± Î·) ?_ (map_mul_Î± Î·)
  let Î±_inv : (G â†’ k) â†’ (G â†’ k) := (Î·.inv.hom.app (fdRepÏ„áµ£ k G)).hom
  have := Î·.inv_hom_id
  apply_fun NatTrans.app âˆ˜ LaxMonoidalFunctor.Hom.hom at this
  replace := congrFun this (fdRepÏ„áµ£ k G)
  apply_fun (Hom.hom Â· (1 : G â†’ k)) at this
  change (Î± Î·) (Î±_inv 1) = (1 : G â†’ k) at this
  have h := this
  rwa [â† one_mul (Î±_inv 1), map_mul_Î±, h, mul_one] at this

end TannakaDuality
