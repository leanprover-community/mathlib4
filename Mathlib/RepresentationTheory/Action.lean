/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.Algebra.Category.GroupCat.Basic
import Mathlib.CategoryTheory.SingleObj
import Mathlib.CategoryTheory.Limits.FunctorCategory
import Mathlib.CategoryTheory.Limits.Preserves.Basic
import Mathlib.CategoryTheory.Adjunction.Limits
import Mathlib.CategoryTheory.Monoidal.FunctorCategory
import Mathlib.CategoryTheory.Monoidal.Transport
import Mathlib.CategoryTheory.Monoidal.Rigid.OfEquivalence
import Mathlib.CategoryTheory.Monoidal.Rigid.FunctorCategory
import Mathlib.CategoryTheory.Monoidal.Linear
import Mathlib.CategoryTheory.Monoidal.Braided
import Mathlib.CategoryTheory.Monoidal.Types.Symmetric
import Mathlib.CategoryTheory.Abelian.FunctorCategory
import Mathlib.CategoryTheory.Abelian.Transfer
import Mathlib.CategoryTheory.Conj
import Mathlib.CategoryTheory.Linear.FunctorCategory

#align_import representation_theory.Action from "leanprover-community/mathlib"@"95a87616d63b3cb49d3fe678d416fbe9c4217bf4"

/-!
# `Action V G`, the category of actions of a monoid `G` inside some category `V`.

The prototypical example is `V = ModuleCat R`,
where `Action (ModuleCat R) G` is the category of `R`-linear representations of `G`.

We check `Action V G ‚âå (singleObj G ‚•§ V)`,
and construct the restriction functors `res {G H : Mon} (f : G ‚ü∂ H) : Action V H ‚•§ Action V G`.

* When `V` has (co)limits so does `Action V G`.
* When `V` is monoidal, braided, or symmetric, so is `Action V G`.
* When `V` is preadditive, linear, or abelian so is `Action V G`.
-/


universe u v

open CategoryTheory Limits

variable (V : Type (u + 1)) [LargeCategory V]

-- Note: this is _not_ a categorical action of `G` on `V`.
/-- An `Action V G` represents a bundled action of
the monoid `G` on an object of some category `V`.

As an example, when `V = ModuleCat R`, this is an `R`-linear representation of `G`,
while when `V = Type` this is a `G`-action.
-/
structure Action (G : MonCat.{u}) where
  V : V
  œÅ : G ‚ü∂ MonCat.of (End V)
set_option linter.uppercaseLean3 false in
#align Action Action

namespace Action

variable {V}

@[simp 1100]
theorem œÅ_one {G : MonCat.{u}} (A : Action V G) : A.œÅ 1 = ùüô A.V := by rw [MonoidHom.map_one]; rfl
set_option linter.uppercaseLean3 false in
#align Action.œÅ_one Action.œÅ_one

/-- When a group acts, we can lift the action to the group of automorphisms. -/
@[simps]
def œÅAut {G : GroupCat.{u}} (A : Action V (MonCat.of G)) : G ‚ü∂ GroupCat.of (Aut A.V) where
  toFun g :=
    { hom := A.œÅ g
      inv := A.œÅ (g‚Åª¬π : G)
      hom_inv_id := (A.œÅ.map_mul (g‚Åª¬π : G) g).symm.trans (by rw [inv_mul_self, œÅ_one])
      inv_hom_id := (A.œÅ.map_mul g (g‚Åª¬π : G)).symm.trans (by rw [mul_inv_self, œÅ_one]) }
  map_one' := Aut.ext A.œÅ.map_one
  map_mul' x y := Aut.ext (A.œÅ.map_mul x y)
set_option linter.uppercaseLean3 false in
#align Action.œÅ_Aut Action.œÅAut

variable (G : MonCat.{u})

section

instance inhabited' : Inhabited (Action (Type u) G) :=
  ‚ü®‚ü®PUnit, 1‚ü©‚ü©
set_option linter.uppercaseLean3 false in
#align Action.inhabited' Action.inhabited'

/-- The trivial representation of a group. -/
def trivial : Action AddCommGroupCat G where
  V := AddCommGroupCat.of PUnit
  œÅ := 1
set_option linter.uppercaseLean3 false in
#align Action.trivial Action.trivial

instance : Inhabited (Action AddCommGroupCat G) :=
  ‚ü®trivial G‚ü©

end

variable {G}

/-- A homomorphism of `Action V G`s is a morphism between the underlying objects,
commuting with the action of `G`.
-/
@[ext]
structure Hom (M N : Action V G) where
  hom : M.V ‚ü∂ N.V
  comm : ‚àÄ g : G, M.œÅ g ‚â´ hom = hom ‚â´ N.œÅ g := by aesop_cat
set_option linter.uppercaseLean3 false in
#align Action.hom Action.Hom

namespace Hom

attribute [reassoc] comm
attribute [local simp] comm comm_assoc

/-- The identity morphism on an `Action V G`. -/
@[simps]
def id (M : Action V G) : Action.Hom M M where hom := ùüô M.V
set_option linter.uppercaseLean3 false in
#align Action.hom.id Action.Hom.id

instance (M : Action V G) : Inhabited (Action.Hom M M) :=
  ‚ü®id M‚ü©

/-- The composition of two `Action V G` homomorphisms is the composition of the underlying maps.
-/
@[simps]
def comp {M N K : Action V G} (p : Action.Hom M N) (q : Action.Hom N K) : Action.Hom M K where
  hom := p.hom ‚â´ q.hom
set_option linter.uppercaseLean3 false in
#align Action.hom.comp Action.Hom.comp

end Hom

instance : Category (Action V G) where
  Hom M N := Hom M N
  id M := Hom.id M
  comp f g := Hom.comp f g

-- porting note: added because `Hom.ext` is not triggered automatically
@[ext]
lemma hom_ext {M N : Action V G} (œÜ‚ÇÅ œÜ‚ÇÇ : M ‚ü∂ N) (h : œÜ‚ÇÅ.hom = œÜ‚ÇÇ.hom) : œÜ‚ÇÅ = œÜ‚ÇÇ :=
  Hom.ext _ _ h

@[simp]
theorem id_hom (M : Action V G) : (ùüô M : Hom M M).hom = ùüô M.V :=
  rfl
set_option linter.uppercaseLean3 false in
#align Action.id_hom Action.id_hom

@[simp]
theorem comp_hom {M N K : Action V G} (f : M ‚ü∂ N) (g : N ‚ü∂ K) :
    (f ‚â´ g : Hom M K).hom = f.hom ‚â´ g.hom :=
  rfl
set_option linter.uppercaseLean3 false in
#align Action.comp_hom Action.comp_hom

/-- Construct an isomorphism of `G` actions/representations
from an isomorphism of the underlying objects,
where the forward direction commutes with the group action. -/
@[simps]
def mkIso {M N : Action V G} (f : M.V ‚âÖ N.V)
    (comm : ‚àÄ g : G, M.œÅ g ‚â´ f.hom = f.hom ‚â´ N.œÅ g := by aesop_cat) : M ‚âÖ N where
  hom :=
    { hom := f.hom
      comm := comm }
  inv :=
    { hom := f.inv
      comm := fun g => by have w := comm g =‚â´ f.inv; simp at w; simp [w] }
set_option linter.uppercaseLean3 false in
#align Action.mk_iso Action.mkIso

instance (priority := 100) isIso_of_hom_isIso {M N : Action V G} (f : M ‚ü∂ N) [IsIso f.hom] :
    IsIso f := IsIso.of_iso (mkIso (asIso f.hom) f.comm)
set_option linter.uppercaseLean3 false in
#align Action.is_iso_of_hom_is_iso Action.isIso_of_hom_isIso

instance isIso_hom_mk {M N : Action V G} (f : M.V ‚ü∂ N.V) [IsIso f] (w) :
    @IsIso _ _ M N (Hom.mk f w) :=
  IsIso.of_iso (mkIso (asIso f) w)
set_option linter.uppercaseLean3 false in
#align Action.is_iso_hom_mk Action.isIso_hom_mk

namespace FunctorCategoryEquivalence

/-- Auxilliary definition for `functorCategoryEquivalence`. -/
@[simps]
def functor : Action V G ‚•§ SingleObj G ‚•§ V where
  obj M :=
    { obj := fun _ => M.V
      map := fun g => M.œÅ g
      map_id := fun _ => M.œÅ.map_one
      map_comp := fun g h => M.œÅ.map_mul h g }
  map f :=
    { app := fun _ => f.hom
      naturality := fun _ _ g => f.comm g }
set_option linter.uppercaseLean3 false in
#align Action.functor_category_equivalence.functor Action.FunctorCategoryEquivalence.functor

/-- Auxilliary definition for `functorCategoryEquivalence`. -/
@[simps]
def inverse : (SingleObj G ‚•§ V) ‚•§ Action V G where
  obj F :=
    { V := F.obj PUnit.unit
      œÅ :=
        { toFun := fun g => F.map g
          map_one' := F.map_id PUnit.unit
          map_mul' := fun g h => F.map_comp h g } }
  map f :=
    { hom := f.app PUnit.unit
      comm := fun g => f.naturality g }
set_option linter.uppercaseLean3 false in
#align Action.functor_category_equivalence.inverse Action.FunctorCategoryEquivalence.inverse

/-- Auxilliary definition for `functorCategoryEquivalence`. -/
@[simps!]
def unitIso : ùü≠ (Action V G) ‚âÖ functor ‚ãô inverse :=
  NatIso.ofComponents fun M => mkIso (Iso.refl _)
set_option linter.uppercaseLean3 false in
#align Action.functor_category_equivalence.unit_iso Action.FunctorCategoryEquivalence.unitIso

/-- Auxilliary definition for `functorCategoryEquivalence`. -/
@[simps!]
def counitIso : inverse ‚ãô functor ‚âÖ ùü≠ (SingleObj G ‚•§ V) :=
  NatIso.ofComponents fun M => NatIso.ofComponents fun X => Iso.refl _
set_option linter.uppercaseLean3 false in
#align Action.functor_category_equivalence.counit_iso Action.FunctorCategoryEquivalence.counitIso

end FunctorCategoryEquivalence

section

open FunctorCategoryEquivalence

variable (V G)

/-- The category of actions of `G` in the category `V`
is equivalent to the functor category `singleObj G ‚•§ V`.
-/
@[simps]
def functorCategoryEquivalence : Action V G ‚âå SingleObj G ‚•§ V where
  functor := functor
  inverse := inverse
  unitIso := unitIso
  counitIso := counitIso
set_option linter.uppercaseLean3 false in
#align Action.functor_category_equivalence Action.functorCategoryEquivalence
set_option linter.uppercaseLean3 false in
#align Action.functor_category_equivalence.functor_def Action.functorCategoryEquivalence_functor
set_option linter.uppercaseLean3 false in
#align Action.functor_category_equivalence.inverse_def Action.functorCategoryEquivalence_inverse

/-
porting note: these two lemmas are redundant with the projections created by the @[simps]
attribute above

theorem functorCategoryEquivalence.functor_def :
    (functorCategoryEquivalence V G).functor = FunctorCategoryEquivalence.functor :=
  rfl
set_option linter.uppercaseLean3 false in
#align Action.functor_category_equivalence.functor_def Action.functorCategoryEquivalence.functor_def

theorem functorCategoryEquivalence.inverse_def :
    (functorCategoryEquivalence V G).inverse = FunctorCategoryEquivalence.inverse :=
  rfl
set_option linter.uppercaseLean3 false in
#align Action.functor_category_equivalence.inverse_def Action.functorCategoryEquivalence.inverse_def-/

instance [HasFiniteProducts V] : HasFiniteProducts (Action V G) where
  out _ :=
    Adjunction.hasLimitsOfShape_of_equivalence (Action.functorCategoryEquivalence _ _).functor

instance [HasFiniteLimits V] : HasFiniteLimits (Action V G) where
  out _ _ _ :=
    Adjunction.hasLimitsOfShape_of_equivalence (Action.functorCategoryEquivalence _ _).functor

instance [HasLimits V] : HasLimits (Action V G) :=
  Adjunction.has_limits_of_equivalence (Action.functorCategoryEquivalence _ _).functor

instance [HasColimits V] : HasColimits (Action V G) :=
  Adjunction.has_colimits_of_equivalence (Action.functorCategoryEquivalence _ _).functor

end

section Forget

variable (V G)

/-- (implementation) The forgetful functor from bundled actions to the underlying objects.

Use the `CategoryTheory.forget` API provided by the `ConcreteCategory` instance below,
rather than using this directly.
-/
@[simps]
def forget : Action V G ‚•§ V where
  obj M := M.V
  map f := f.hom
set_option linter.uppercaseLean3 false in
#align Action.forget Action.forget

instance : Faithful (forget V G) where map_injective w := Hom.ext _ _ w

instance [ConcreteCategory V] : ConcreteCategory (Action V G) where
  forget := forget V G ‚ãô ConcreteCategory.forget

instance hasForgetToV [ConcreteCategory V] : HasForget‚ÇÇ (Action V G) V where forget‚ÇÇ := forget V G
set_option linter.uppercaseLean3 false in
#align Action.has_forget_to_V Action.hasForgetToV

/-- The forgetful functor is intertwined by `functorCategoryEquivalence` with
evaluation at `PUnit.star`. -/
def functorCategoryEquivalenceCompEvaluation :
    (functorCategoryEquivalence V G).functor ‚ãô (evaluation _ _).obj PUnit.unit ‚âÖ forget V G :=
  Iso.refl _
set_option linter.uppercaseLean3 false in
#align Action.functor_category_equivalence_comp_evaluation Action.functorCategoryEquivalenceCompEvaluation

noncomputable instance instPreservesLimitsForget [HasLimits V] :
    Limits.PreservesLimits (forget V G) :=
  Limits.preservesLimitsOfNatIso (Action.functorCategoryEquivalenceCompEvaluation V G)

noncomputable instance instPreservesColimitsForget [HasColimits V] :
    PreservesColimits (forget V G) :=
  preservesColimitsOfNatIso (Action.functorCategoryEquivalenceCompEvaluation V G)

-- TODO construct categorical images?
end Forget

theorem Iso.conj_œÅ {M N : Action V G} (f : M ‚âÖ N) (g : G) :
    N.œÅ g = ((forget V G).mapIso f).conj (M.œÅ g) :=
      by rw [Iso.conj_apply, Iso.eq_inv_comp]; simp [f.hom.comm]
set_option linter.uppercaseLean3 false in
#align Action.iso.conj_œÅ Action.Iso.conj_œÅ

section HasZeroMorphisms

variable [HasZeroMorphisms V]

-- porting note: in order to ease automation, the `Zero` instance is introduced separately,
-- and the lemma `zero_hom` was moved just below
instance {X Y : Action V G} : Zero (X ‚ü∂ Y) := ‚ü®0, by aesop_cat‚ü©

@[simp]
theorem zero_hom {X Y : Action V G} : (0 : X ‚ü∂ Y).hom = 0 :=
  rfl
set_option linter.uppercaseLean3 false in
#align Action.zero_hom Action.zero_hom

instance : HasZeroMorphisms (Action V G) where

instance forget_preservesZeroMorphisms : Functor.PreservesZeroMorphisms (forget V G) where
set_option linter.uppercaseLean3 false in
#align Action.forget_preserves_zero_morphisms Action.forget_preservesZeroMorphisms

instance forget‚ÇÇ_preservesZeroMorphisms [ConcreteCategory V] :
    Functor.PreservesZeroMorphisms (forget‚ÇÇ (Action V G) V) where
set_option linter.uppercaseLean3 false in
#align Action.forget‚ÇÇ_preserves_zero_morphisms Action.forget‚ÇÇ_preservesZeroMorphisms

instance functorCategoryEquivalence_preservesZeroMorphisms :
    Functor.PreservesZeroMorphisms (functorCategoryEquivalence V G).functor where
set_option linter.uppercaseLean3 false in
#align Action.functor_category_equivalence_preserves_zero_morphisms Action.functorCategoryEquivalence_preservesZeroMorphisms

end HasZeroMorphisms

section Preadditive

variable [Preadditive V]

instance : Preadditive (Action V G) where
  homGroup X Y :=
    { add := fun f g => ‚ü®f.hom + g.hom, by simp [f.comm, g.comm]‚ü©
      neg := fun f => ‚ü®-f.hom, by simp [f.comm]‚ü©
      zero_add := by intros; ext; exact zero_add _
      add_zero := by intros; ext; exact add_zero _
      add_assoc := by intros; ext; exact add_assoc _ _ _
      add_left_neg := by intros; ext; exact add_left_neg _
      add_comm := by intros; ext; exact add_comm _ _ }
  add_comp := by intros; ext; exact Preadditive.add_comp _ _ _ _ _ _
  comp_add := by intros; ext; exact Preadditive.comp_add _ _ _ _ _ _

instance forget_additive : Functor.Additive (forget V G) where
set_option linter.uppercaseLean3 false in
#align Action.forget_additive Action.forget_additive

instance forget‚ÇÇ_additive [ConcreteCategory V] : Functor.Additive (forget‚ÇÇ (Action V G) V) where
set_option linter.uppercaseLean3 false in
#align Action.forget‚ÇÇ_additive Action.forget‚ÇÇ_additive

instance functorCategoryEquivalence_additive :
    Functor.Additive (functorCategoryEquivalence V G).functor where
set_option linter.uppercaseLean3 false in
#align Action.functor_category_equivalence_additive Action.functorCategoryEquivalence_additive

@[simp]
theorem neg_hom {X Y : Action V G} (f : X ‚ü∂ Y) : (-f).hom = -f.hom :=
  rfl
set_option linter.uppercaseLean3 false in
#align Action.neg_hom Action.neg_hom

@[simp]
theorem add_hom {X Y : Action V G} (f g : X ‚ü∂ Y) : (f + g).hom = f.hom + g.hom :=
  rfl
set_option linter.uppercaseLean3 false in
#align Action.add_hom Action.add_hom

@[simp]
theorem sum_hom {X Y : Action V G} {Œπ : Type*} (f : Œπ ‚Üí (X ‚ü∂ Y)) (s : Finset Œπ) :
    (s.sum f).hom = s.sum fun i => (f i).hom :=
  (forget V G).map_sum f s
set_option linter.uppercaseLean3 false in
#align Action.sum_hom Action.sum_hom

end Preadditive

section Linear

variable [Preadditive V] {R : Type*} [Semiring R] [Linear R V]

instance : Linear R (Action V G) where
  homModule X Y :=
    { smul := fun r f => ‚ü®r ‚Ä¢ f.hom, by simp [f.comm]‚ü©
      one_smul := by intros; ext; exact one_smul _ _
      smul_zero := by intros; ext; exact smul_zero _
      zero_smul := by intros; ext; exact zero_smul _ _
      add_smul := by intros; ext; exact add_smul _ _ _
      smul_add := by intros; ext; exact smul_add _ _ _
      mul_smul := by intros; ext; exact mul_smul _ _ _ }
  smul_comp := by intros; ext; exact Linear.smul_comp _ _ _ _ _ _
  comp_smul := by intros; ext; exact Linear.comp_smul _ _ _ _ _ _

instance forget_linear : Functor.Linear R (forget V G) where
set_option linter.uppercaseLean3 false in
#align Action.forget_linear Action.forget_linear

instance forget‚ÇÇ_linear [ConcreteCategory V] : Functor.Linear R (forget‚ÇÇ (Action V G) V) where
set_option linter.uppercaseLean3 false in
#align Action.forget‚ÇÇ_linear Action.forget‚ÇÇ_linear

instance functorCategoryEquivalence_linear :
    Functor.Linear R (functorCategoryEquivalence V G).functor where
set_option linter.uppercaseLean3 false in
#align Action.functor_category_equivalence_linear Action.functorCategoryEquivalence_linear

@[simp]
theorem smul_hom {X Y : Action V G} (r : R) (f : X ‚ü∂ Y) : (r ‚Ä¢ f).hom = r ‚Ä¢ f.hom :=
  rfl
set_option linter.uppercaseLean3 false in
#align Action.smul_hom Action.smul_hom

end Linear

section Abelian

/-- Auxilliary construction for the `Abelian (Action V G)` instance. -/
def abelianAux : Action V G ‚âå ULift.{u} (SingleObj G) ‚•§ V :=
  (functorCategoryEquivalence V G).trans (Equivalence.congrLeft ULift.equivalence)
set_option linter.uppercaseLean3 false in
#align Action.abelian_aux Action.abelianAux

noncomputable instance [Abelian V] : Abelian (Action V G) :=
  abelianOfEquivalence abelianAux.functor

end Abelian

section Monoidal

open MonoidalCategory

variable [MonoidalCategory V]

instance : MonoidalCategory (Action V G) :=
  Monoidal.transport (Action.functorCategoryEquivalence _ _).symm

@[simp]
theorem tensorUnit_v : (ùüô_ (Action V G)).V = ùüô_ V :=
  rfl
set_option linter.uppercaseLean3 false in
#align Action.tensor_unit_V Action.tensorUnit_v

-- porting note: removed @[simp] as the simpNF linter complains
theorem tensorUnit_rho {g : G} : (ùüô_ (Action V G)).œÅ g = ùüô (ùüô_ V) :=
  rfl
set_option linter.uppercaseLean3 false in
#align Action.tensor_unit_rho Action.tensorUnit_rho

@[simp]
theorem tensor_v {X Y : Action V G} : (X ‚äó Y).V = X.V ‚äó Y.V :=
  rfl
set_option linter.uppercaseLean3 false in
#align Action.tensor_V Action.tensor_v

-- porting note: removed @[simp] as the simpNF linter complains
theorem tensor_rho {X Y : Action V G} {g : G} : (X ‚äó Y).œÅ g = X.œÅ g ‚äó Y.œÅ g :=
  rfl
set_option linter.uppercaseLean3 false in
#align Action.tensor_rho Action.tensor_rho

@[simp]
theorem tensorHom {W X Y Z : Action V G} (f : W ‚ü∂ X) (g : Y ‚ü∂ Z) : (f ‚äó g).hom = f.hom ‚äó g.hom :=
  rfl
set_option linter.uppercaseLean3 false in
#align Action.tensor_hom Action.tensorHom

-- porting note: removed @[simp] as the simpNF linter complains
theorem associator_hom_hom {X Y Z : Action V G} :
    Hom.hom (Œ±_ X Y Z).hom = (Œ±_ X.V Y.V Z.V).hom := by
  dsimp [Monoidal.transport_associator]
  simp
set_option linter.uppercaseLean3 false in
#align Action.associator_hom_hom Action.associator_hom_hom

-- porting note: removed @[simp] as the simpNF linter complains
theorem associator_inv_hom {X Y Z : Action V G} :
    Hom.hom (Œ±_ X Y Z).inv = (Œ±_ X.V Y.V Z.V).inv := by
  dsimp [Monoidal.transport_associator]
  simp
set_option linter.uppercaseLean3 false in
#align Action.associator_inv_hom Action.associator_inv_hom

-- porting note: removed @[simp] as the simpNF linter complains
theorem leftUnitor_hom_hom {X : Action V G} : Hom.hom (Œª_ X).hom = (Œª_ X.V).hom := by
  dsimp [Monoidal.transport_leftUnitor]
  simp
set_option linter.uppercaseLean3 false in
#align Action.left_unitor_hom_hom Action.leftUnitor_hom_hom

-- porting note: removed @[simp] as the simpNF linter complains
theorem leftUnitor_inv_hom {X : Action V G} : Hom.hom (Œª_ X).inv = (Œª_ X.V).inv := by
  dsimp [Monoidal.transport_leftUnitor]
  simp
set_option linter.uppercaseLean3 false in
#align Action.left_unitor_inv_hom Action.leftUnitor_inv_hom

-- porting note: removed @[simp] as the simpNF linter complains
theorem rightUnitor_hom_hom {X : Action V G} : Hom.hom (œÅ_ X).hom = (œÅ_ X.V).hom := by
  dsimp [Monoidal.transport_rightUnitor]
  simp
set_option linter.uppercaseLean3 false in
#align Action.right_unitor_hom_hom Action.rightUnitor_hom_hom

-- porting note: removed @[simp] as the simpNF linter complains
theorem rightUnitor_inv_hom {X : Action V G} : Hom.hom (œÅ_ X).inv = (œÅ_ X.V).inv := by
  dsimp [Monoidal.transport_rightUnitor]
  simp
set_option linter.uppercaseLean3 false in
#align Action.right_unitor_inv_hom Action.rightUnitor_inv_hom

/-- Given an object `X` isomorphic to the tensor unit of `V`, `X` equipped with the trivial action
is isomorphic to the tensor unit of `Action V G`. -/
def tensorUnitIso {X : V} (f : ùüô_ V ‚âÖ X) : ùüô_ (Action V G) ‚âÖ Action.mk X 1 :=
  Action.mkIso f fun _ => by
    simp only [MonoidHom.one_apply, End.one_def, Category.id_comp f.hom, tensorUnit_rho,
      MonCat.oneHom_apply, MonCat.one_of, Category.comp_id]
set_option linter.uppercaseLean3 false in
#align Action.tensor_unit_iso Action.tensorUnitIso

variable (V G)

/-- When `V` is monoidal the forgetful functor `Action V G` to `V` is monoidal. -/
@[simps]
def forgetMonoidal : MonoidalFunctor (Action V G) V :=
  { Action.forget _ _ with
    Œµ := ùüô _
    Œº := fun X Y => ùüô _ }
set_option linter.uppercaseLean3 false in
#align Action.forget_monoidal Action.forgetMonoidal

instance forgetMonoidal_faithful : Faithful (forgetMonoidal V G).toFunctor := by
  change Faithful (forget V G); infer_instance
set_option linter.uppercaseLean3 false in
#align Action.forget_monoidal_faithful Action.forgetMonoidal_faithful

section

variable [BraidedCategory V]

instance : BraidedCategory (Action V G) :=
  braidedCategoryOfFaithful (forgetMonoidal V G) (fun X Y => mkIso (Œ≤_ _ _)
    (fun g => by simp [FunctorCategoryEquivalence.inverse])) (by aesop_cat)

/-- When `V` is braided the forgetful functor `Action V G` to `V` is braided. -/
@[simps!]
def forgetBraided : BraidedFunctor (Action V G) V :=
  { forgetMonoidal _ _ with }
set_option linter.uppercaseLean3 false in
#align Action.forget_braided Action.forgetBraided

instance forgetBraided_faithful : Faithful (forgetBraided V G).toFunctor := by
  change Faithful (forget V G); infer_instance
set_option linter.uppercaseLean3 false in
#align Action.forget_braided_faithful Action.forgetBraided_faithful

end

instance [SymmetricCategory V] : SymmetricCategory (Action V G) :=
  symmetricCategoryOfFaithful (forgetBraided V G)

section

variable [Preadditive V] [MonoidalPreadditive V]

attribute [local simp] MonoidalPreadditive.tensor_add MonoidalPreadditive.add_tensor

instance : MonoidalPreadditive (Action V G) where

variable {R : Type*} [Semiring R] [Linear R V] [MonoidalLinear R V]

instance : MonoidalLinear R (Action V G) where

end

noncomputable section

/-- Upgrading the functor `Action V G ‚•§ (SingleObj G ‚•§ V)` to a monoidal functor. -/
def functorCategoryMonoidalEquivalence : MonoidalFunctor (Action V G) (SingleObj G ‚•§ V) :=
  Monoidal.fromTransported (Action.functorCategoryEquivalence _ _).symm
set_option linter.uppercaseLean3 false in
#align Action.functor_category_monoidal_equivalence Action.functorCategoryMonoidalEquivalence

instance : IsEquivalence (functorCategoryMonoidalEquivalence V G).toFunctor := by
  change IsEquivalence (Action.functorCategoryEquivalence _ _).functor; infer_instance

@[simp]
theorem functorCategoryMonoidalEquivalence.Œº_app (A B : Action V G) :
    ((functorCategoryMonoidalEquivalence V G).Œº A B).app PUnit.unit = ùüô _ := by
  dsimp only [functorCategoryMonoidalEquivalence]
  simp only [Monoidal.fromTransported_toLaxMonoidalFunctor_Œº, NatTrans.comp_app]
  -- porting note: Lean3 was able to see through some defeq, as the mathlib3 proof was
  --   show (ùüô A.V ‚äó ùüô B.V) ‚â´ ùüô (A.V ‚äó B.V) ‚â´ (ùüô A.V ‚äó ùüô B.V) = ùüô (A.V ‚äó B.V)
  --   simp only [monoidal_category.tensor_id, category.comp_id]
  dsimp [Equivalence.unit]
  erw [Category.id_comp]
  rw [NatIso.isIso_inv_app, IsIso.inv_comp_eq]
  erw [MonoidalCategory.tensor_id]
  erw [(functorCategoryEquivalence V G).inverse.map_id,
    (functorCategoryEquivalence V G).functor.map_id, Category.id_comp]
  rfl
set_option linter.uppercaseLean3 false in
#align Action.functor_category_monoidal_equivalence.Œº_app Action.functorCategoryMonoidalEquivalence.Œº_app

@[simp]
theorem functorCategoryMonoidalEquivalence.ŒºIso_inv_app (A B : Action V G) :
    ((functorCategoryMonoidalEquivalence V G).ŒºIso A B).inv.app PUnit.unit = ùüô _ := by
  rw [‚Üê NatIso.app_inv, ‚Üê IsIso.Iso.inv_hom]
  refine' IsIso.inv_eq_of_hom_inv_id _
  rw [Category.comp_id, NatIso.app_hom, MonoidalFunctor.ŒºIso_hom,
    functorCategoryMonoidalEquivalence.Œº_app]
set_option linter.uppercaseLean3 false in
#align Action.functor_category_monoidal_equivalence.Œº_iso_inv_app Action.functorCategoryMonoidalEquivalence.ŒºIso_inv_app

@[simp]
theorem functorCategoryMonoidalEquivalence.Œµ_app :
    (functorCategoryMonoidalEquivalence V G).Œµ.app PUnit.unit = ùüô _ := by
  dsimp only [functorCategoryMonoidalEquivalence]
  simp only [Monoidal.fromTransported_toLaxMonoidalFunctor_Œµ]
  rfl
set_option linter.uppercaseLean3 false in
#align Action.functor_category_monoidal_equivalence.Œµ_app Action.functorCategoryMonoidalEquivalence.Œµ_app

@[simp]
theorem functorCategoryMonoidalEquivalence.inv_counit_app_hom (A : Action V G) :
    ((functorCategoryMonoidalEquivalence _ _).inv.adjunction.counit.app A).hom = ùüô _ :=
  rfl
set_option linter.uppercaseLean3 false in
#align Action.functor_category_monoidal_equivalence.inv_counit_app_hom Action.functorCategoryMonoidalEquivalence.inv_counit_app_hom

@[simp]
theorem functorCategoryMonoidalEquivalence.counit_app (A : SingleObj G ‚•§ V) :
    ((functorCategoryMonoidalEquivalence _ _).adjunction.counit.app A).app PUnit.unit = ùüô _ :=
  rfl
set_option linter.uppercaseLean3 false in
#align Action.functor_category_monoidal_equivalence.counit_app Action.functorCategoryMonoidalEquivalence.counit_app

@[simp]
theorem functorCategoryMonoidalEquivalence.inv_unit_app_app (A : SingleObj G ‚•§ V) :
    ((functorCategoryMonoidalEquivalence _ _).inv.adjunction.unit.app A).app PUnit.unit = ùüô _ :=
  rfl
set_option linter.uppercaseLean3 false in
#align Action.functor_category_monoidal_equivalence.inv_unit_app_app Action.functorCategoryMonoidalEquivalence.inv_unit_app_app

@[simp]
theorem functorCategoryMonoidalEquivalence.unit_app_hom (A : Action V G) :
    ((functorCategoryMonoidalEquivalence _ _).adjunction.unit.app A).hom = ùüô _ :=
  rfl
set_option linter.uppercaseLean3 false in
#align Action.functor_category_monoidal_equivalence.unit_app_hom Action.functorCategoryMonoidalEquivalence.unit_app_hom

@[simp]
theorem functorCategoryMonoidalEquivalence.functor_map {A B : Action V G} (f : A ‚ü∂ B) :
    (functorCategoryMonoidalEquivalence _ _).map f = FunctorCategoryEquivalence.functor.map f :=
  rfl
set_option linter.uppercaseLean3 false in
#align Action.functor_category_monoidal_equivalence.functor_map Action.functorCategoryMonoidalEquivalence.functor_map

@[simp]
theorem functorCategoryMonoidalEquivalence.inverse_map {A B : SingleObj G ‚•§ V} (f : A ‚ü∂ B) :
    (functorCategoryMonoidalEquivalence _ _).inv.map f = FunctorCategoryEquivalence.inverse.map f :=
  rfl
set_option linter.uppercaseLean3 false in
#align Action.functor_category_monoidal_equivalence.inverse_map Action.functorCategoryMonoidalEquivalence.inverse_map

variable (H : GroupCat.{u})

instance [RightRigidCategory V] : RightRigidCategory (SingleObj (H : MonCat.{u}) ‚•§ V) := by
  change RightRigidCategory (SingleObj H ‚•§ V); infer_instance

/-- If `V` is right rigid, so is `Action V G`. -/
instance [RightRigidCategory V] : RightRigidCategory (Action V H) :=
  rightRigidCategoryOfEquivalence (functorCategoryMonoidalEquivalence V _)

instance [LeftRigidCategory V] : LeftRigidCategory (SingleObj (H : MonCat.{u}) ‚•§ V) := by
  change LeftRigidCategory (SingleObj H ‚•§ V); infer_instance

/-- If `V` is left rigid, so is `Action V G`. -/
instance [LeftRigidCategory V] : LeftRigidCategory (Action V H) :=
  leftRigidCategoryOfEquivalence (functorCategoryMonoidalEquivalence V _)

instance [RigidCategory V] : RigidCategory (SingleObj (H : MonCat.{u}) ‚•§ V) := by
  change RigidCategory (SingleObj H ‚•§ V); infer_instance

/-- If `V` is rigid, so is `Action V G`. -/
instance [RigidCategory V] : RigidCategory (Action V H) :=
  rigidCategoryOfEquivalence (functorCategoryMonoidalEquivalence V _)

variable {V H}
variable (X : Action V H)

@[simp]
theorem rightDual_v [RightRigidCategory V] : X·òÅ.V = X.V·òÅ :=
  rfl
set_option linter.uppercaseLean3 false in
#align Action.right_dual_V Action.rightDual_v

@[simp]
theorem leftDual_v [LeftRigidCategory V] : (·òÅX).V = ·òÅX.V :=
  rfl
set_option linter.uppercaseLean3 false in
#align Action.left_dual_V Action.leftDual_v

@[simp]
theorem rightDual_œÅ [RightRigidCategory V] (h : H) : X·òÅ.œÅ h = (X.œÅ (h‚Åª¬π : H))·òÅ := by
  rw [‚Üê SingleObj.inv_as_inv]; rfl
set_option linter.uppercaseLean3 false in
#align Action.right_dual_œÅ Action.rightDual_œÅ

@[simp]
theorem leftDual_œÅ [LeftRigidCategory V] (h : H) : (·òÅX).œÅ h = ·òÅX.œÅ (h‚Åª¬π : H) := by
  rw [‚Üê SingleObj.inv_as_inv]; rfl
set_option linter.uppercaseLean3 false in
#align Action.left_dual_œÅ Action.leftDual_œÅ

end

end Monoidal

/-- Actions/representations of the trivial group are just objects in the ambient category. -/
def actionPunitEquivalence : Action V (MonCat.of PUnit) ‚âå V where
  functor := forget V _
  inverse :=
    { obj := fun X => ‚ü®X, 1‚ü©
      map := fun f => ‚ü®f, fun ‚ü®‚ü© => by simp‚ü© }
  unitIso :=
    NatIso.ofComponents fun X => mkIso (Iso.refl _) fun ‚ü®‚ü© => by
      simp only [MonCat.oneHom_apply, MonCat.one_of, End.one_def, id_eq, Functor.comp_obj,
        forget_obj, Iso.refl_hom, Category.comp_id]
      exact œÅ_one X
  counitIso := NatIso.ofComponents fun X => Iso.refl _
set_option linter.uppercaseLean3 false in
#align Action.Action_punit_equivalence Action.actionPunitEquivalence

variable (V)

/-- The "restriction" functor along a monoid homomorphism `f : G ‚ü∂ H`,
taking actions of `H` to actions of `G`.

(This makes sense for any homomorphism, but the name is natural when `f` is a monomorphism.)
-/
@[simps]
def res {G H : MonCat} (f : G ‚ü∂ H) : Action V H ‚•§ Action V G where
  obj M :=
    { V := M.V
      œÅ := f ‚â´ M.œÅ }
  map p :=
    { hom := p.hom
      comm := fun g => p.comm (f g) }
set_option linter.uppercaseLean3 false in
#align Action.res Action.res

/-- The natural isomorphism from restriction along the identity homomorphism to
the identity functor on `Action V G`.
-/
@[simps!]
def resId {G : MonCat} : res V (ùüô G) ‚âÖ ùü≠ (Action V G) :=
  NatIso.ofComponents fun M => mkIso (Iso.refl _)
set_option linter.uppercaseLean3 false in
#align Action.res_id Action.resId

/-- The natural isomorphism from the composition of restrictions along homomorphisms
to the restriction along the composition of homomorphism.
-/
@[simps!]
def resComp {G H K : MonCat} (f : G ‚ü∂ H) (g : H ‚ü∂ K) : res V g ‚ãô res V f ‚âÖ res V (f ‚â´ g) :=
  NatIso.ofComponents fun M => mkIso (Iso.refl _)
set_option linter.uppercaseLean3 false in
#align Action.res_comp Action.resComp

-- TODO promote `res` to a pseudofunctor from
-- the locally discrete bicategory constructed from `Mon·µí·µñ` to `Cat`, sending `G` to `Action V G`.
variable {H : MonCat.{u}} (f : G ‚ü∂ H)

instance res_additive [Preadditive V] : (res V f).Additive where
set_option linter.uppercaseLean3 false in
#align Action.res_additive Action.res_additive

variable {R : Type*} [Semiring R]

instance res_linear [Preadditive V] [Linear R V] : (res V f).Linear R where
set_option linter.uppercaseLean3 false in
#align Action.res_linear Action.res_linear

/-- Bundles a type `H` with a multiplicative action of `G` as an `Action`. -/
def ofMulAction (G H : Type u) [Monoid G] [MulAction G H] : Action (Type u) (MonCat.of G) where
  V := H
  œÅ := @MulAction.toEndHom _ _ _ (by assumption)
set_option linter.uppercaseLean3 false in
#align Action.of_mul_action Action.ofMulAction

@[simp]
theorem ofMulAction_apply {G H : Type u} [Monoid G] [MulAction G H] (g : G) (x : H) :
    (ofMulAction G H).œÅ g x = (g ‚Ä¢ x : H) :=
  rfl
set_option linter.uppercaseLean3 false in
#align Action.of_mul_action_apply Action.ofMulAction_apply

/-- Given a family `F` of types with `G`-actions, this is the limit cone demonstrating that the
product of `F` as types is a product in the category of `G`-sets. -/
def ofMulActionLimitCone {Œπ : Type v} (G : Type max v u) [Monoid G] (F : Œπ ‚Üí Type max v u)
    [‚àÄ i : Œπ, MulAction G (F i)] :
    LimitCone (Discrete.functor fun i : Œπ => Action.ofMulAction G (F i)) where
  cone :=
    { pt := Action.ofMulAction G (‚àÄ i : Œπ, F i)
      œÄ := Discrete.natTrans (fun i => ‚ü®fun x => x i.as, fun g => rfl‚ü©) }
  isLimit :=
    { lift := fun s =>
        { hom := fun x i => (s.œÄ.app ‚ü®i‚ü©).hom x
          comm := fun g => by
            ext x
            funext j
            exact congr_fun ((s.œÄ.app ‚ü®j‚ü©).comm g) x }
      fac := fun s j => rfl
      uniq := fun s f h => by
        ext x
        funext j
        dsimp at *
        rw [‚Üê h ‚ü®j‚ü©]
        rfl }
set_option linter.uppercaseLean3 false in
#align Action.of_mul_action_limit_cone Action.ofMulActionLimitCone

/-- The `G`-set `G`, acting on itself by left multiplication. -/
@[simps!]
def leftRegular (G : Type u) [Monoid G] : Action (Type u) (MonCat.of G) :=
  Action.ofMulAction G G
set_option linter.uppercaseLean3 false in
#align Action.left_regular Action.leftRegular

/-- The `G`-set `G‚Åø`, acting on itself by left multiplication. -/
@[simps!]
def diagonal (G : Type u) [Monoid G] (n : ‚Ñï) : Action (Type u) (MonCat.of G) :=
  Action.ofMulAction G (Fin n ‚Üí G)
set_option linter.uppercaseLean3 false in
#align Action.diagonal Action.diagonal

/-- We have `fin 1 ‚Üí G ‚âÖ G` as `G`-sets, with `G` acting by left multiplication. -/
def diagonalOneIsoLeftRegular (G : Type u) [Monoid G] : diagonal G 1 ‚âÖ leftRegular G :=
  Action.mkIso (Equiv.funUnique _ _).toIso fun _ => rfl
set_option linter.uppercaseLean3 false in
#align Action.diagonal_one_iso_left_regular Action.diagonalOneIsoLeftRegular

open MonoidalCategory

/-- Given `X : Action (Type u) (Mon.of G)` for `G` a group, then `G √ó X` (with `G` acting as left
multiplication on the first factor and by `X.œÅ` on the second) is isomorphic as a `G`-set to
`G √ó X` (with `G` acting as left multiplication on the first factor and trivially on the second).
The isomorphism is given by `(g, x) ‚Ü¶ (g, g‚Åª¬π ‚Ä¢ x)`. -/
@[simps]
noncomputable def leftRegularTensorIso (G : Type u) [Group G] (X : Action (Type u) (MonCat.of G)) :
    leftRegular G ‚äó X ‚âÖ leftRegular G ‚äó Action.mk X.V 1 where
  hom :=
    { hom := fun g => ‚ü®g.1, (X.œÅ (g.1‚Åª¬π : G) g.2 : X.V)‚ü©
      comm := fun (g : G) => by
        funext ‚ü®(x‚ÇÅ : G), (x‚ÇÇ : X.V)‚ü©
        refine' Prod.ext rfl _
        change (X.œÅ ((g * x‚ÇÅ)‚Åª¬π : G) * X.œÅ g) x‚ÇÇ = X.œÅ _ _
        rw [mul_inv_rev, ‚Üê X.œÅ.map_mul, inv_mul_cancel_right] }
  inv :=
    { hom := fun g => ‚ü®g.1, X.œÅ g.1 g.2‚ü©
      comm := fun (g : G) => by
        funext ‚ü®(x‚ÇÅ : G), (x‚ÇÇ : X.V)‚ü©
        refine' Prod.ext rfl _
        erw [tensor_rho, tensor_rho]
        dsimp
        rw [leftRegular_œÅ_apply]
        erw [map_mul]
        rfl }
  hom_inv_id := by
    apply Hom.ext
    funext x
    refine' Prod.ext rfl _
    change (X.œÅ x.1 * X.œÅ (x.1‚Åª¬π : G)) x.2 = x.2
    rw [‚Üê X.œÅ.map_mul, mul_inv_self, X.œÅ.map_one, MonCat.one_of, End.one_def, types_id_apply]
  inv_hom_id := by
    apply Hom.ext
    funext x
    refine' Prod.ext rfl _
    change (X.œÅ (x.1‚Åª¬π : G) * X.œÅ x.1) x.2 = x.2
    rw [‚Üê X.œÅ.map_mul, inv_mul_self, X.œÅ.map_one, MonCat.one_of, End.one_def, types_id_apply]
set_option linter.uppercaseLean3 false in
#align Action.left_regular_tensor_iso Action.leftRegularTensorIso

/-- The natural isomorphism of `G`-sets `G‚Åø‚Å∫¬π ‚âÖ G √ó G‚Åø`, where `G` acts by left multiplication on
each factor. -/
@[simps!]
noncomputable def diagonalSucc (G : Type u) [Monoid G] (n : ‚Ñï) :
    diagonal G (n + 1) ‚âÖ leftRegular G ‚äó diagonal G n :=
  mkIso (Equiv.piFinSuccAboveEquiv _ 0).toIso fun _ => rfl
set_option linter.uppercaseLean3 false in
#align Action.diagonal_succ Action.diagonalSucc

end Action

namespace CategoryTheory.Functor

variable {V} {W : Type (u + 1)} [LargeCategory W]

/-- A functor between categories induces a functor between
the categories of `G`-actions within those categories. -/
@[simps]
def mapAction (F : V ‚•§ W) (G : MonCat.{u}) : Action V G ‚•§ Action W G where
  obj M :=
    { V := F.obj M.V
      œÅ :=
        { toFun := fun g => F.map (M.œÅ g)
          map_one' := by simp only [End.one_def, Action.œÅ_one, F.map_id, MonCat.one_of]
          map_mul' := fun g h => by
            dsimp
            rw [map_mul, MonCat.mul_of, End.mul_def, End.mul_def, F.map_comp] } }
  map f :=
    { hom := F.map f.hom
      comm := fun g => by dsimp; rw [‚Üê F.map_comp, f.comm, F.map_comp] }
  map_id M := by ext; simp only [Action.id_hom, F.map_id]
  map_comp f g := by ext; simp only [Action.comp_hom, F.map_comp]
set_option linter.uppercaseLean3 false in
#align category_theory.functor.map_Action CategoryTheory.Functor.mapAction

variable (F : V ‚•§ W) (G : MonCat.{u}) [Preadditive V] [Preadditive W]

instance mapAction_preadditive [F.Additive] : (F.mapAction G).Additive where
set_option linter.uppercaseLean3 false in
#align category_theory.functor.map_Action_preadditive CategoryTheory.Functor.mapAction_preadditive

variable {R : Type*} [Semiring R] [CategoryTheory.Linear R V] [CategoryTheory.Linear R W]

instance mapAction_linear [F.Additive] [F.Linear R] : (F.mapAction G).Linear R where
set_option linter.uppercaseLean3 false in
#align category_theory.functor.map_Action_linear CategoryTheory.Functor.mapAction_linear

end CategoryTheory.Functor

namespace CategoryTheory.MonoidalFunctor

open Action

variable {V}
variable {W : Type (u + 1)} [LargeCategory W] [MonoidalCategory V] [MonoidalCategory W]
  (F : MonoidalFunctor V W) (G : MonCat.{u})

/-- A monoidal functor induces a monoidal functor between
the categories of `G`-actions within those categories. -/
@[simps]
def mapAction : MonoidalFunctor (Action V G) (Action W G) :=
  { F.toFunctor.mapAction G with
    Œµ :=
      { hom := F.Œµ
        comm := fun g => by
          dsimp [FunctorCategoryEquivalence.inverse, Functor.mapAction]
          rw [Category.id_comp, F.map_id, Category.comp_id] }
    Œº := fun X Y =>
      { hom := F.Œº X.V Y.V
        comm := fun g => F.toLaxMonoidalFunctor.Œº_natural (X.œÅ g) (Y.œÅ g) }
    Œµ_isIso := by infer_instance
    Œº_isIso := by infer_instance
    Œº_natural := by intros; ext; simp
    associativity := by intros; ext; simp
    left_unitality := by intros; ext; simp
    right_unitality := by
      intros
      ext
      dsimp
      simp only [MonoidalCategory.rightUnitor_conjugation,
        LaxMonoidalFunctor.right_unitality, Category.id_comp, Category.assoc,
        LaxMonoidalFunctor.right_unitality_inv_assoc, Category.comp_id, Iso.hom_inv_id]
      rw [‚Üê F.map_comp, Iso.inv_hom_id, F.map_id, Category.comp_id] }
set_option linter.uppercaseLean3 false in
#align category_theory.monoidal_functor.map_Action CategoryTheory.MonoidalFunctor.mapAction

@[simp]
theorem mapAction_Œµ_inv_hom : (inv (F.mapAction G).Œµ).hom = inv F.Œµ := by
  rw [‚Üê cancel_mono F.Œµ, IsIso.inv_hom_id, ‚Üê F.mapAction_toLaxMonoidalFunctor_Œµ_hom G,
    ‚Üê Action.comp_hom, IsIso.inv_hom_id, Action.id_hom]
set_option linter.uppercaseLean3 false in
#align category_theory.monoidal_functor.map_Action_Œµ_inv_hom CategoryTheory.MonoidalFunctor.mapAction_Œµ_inv_hom

@[simp]
theorem mapAction_Œº_inv_hom (X Y : Action V G) :
    (inv ((F.mapAction G).Œº X Y)).hom = inv (F.Œº X.V Y.V) := by
  rw [‚Üê cancel_mono (F.Œº X.V Y.V), IsIso.inv_hom_id, ‚Üê F.mapAction_toLaxMonoidalFunctor_Œº_hom G,
    ‚Üê Action.comp_hom, IsIso.inv_hom_id, Action.id_hom]
set_option linter.uppercaseLean3 false in
#align category_theory.monoidal_functor.map_Action_Œº_inv_hom CategoryTheory.MonoidalFunctor.mapAction_Œº_inv_hom

end CategoryTheory.MonoidalFunctor
