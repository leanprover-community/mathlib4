/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.Algebra.MonoidAlgebra.Basic
import Mathlib.Algebra.CharP.Invertible
import Mathlib.LinearAlgebra.Basis.VectorSpace

#align_import representation_theory.maschke from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!
# Maschke's theorem

We prove **Maschke's theorem** for finite groups,
in the formulation that every submodule of a `k[G]` module has a complement,
when `k` is a field with `Invertible (Fintype.card G : k)`.

We do the core computation in greater generality.
For any `[CommRing k]` in which `[Invertible (Fintype.card G : k)]`,
and a `k[G]`-linear map `i : V â†’ W` which admits a `k`-linear retraction `Ï€`,
we produce a `k[G]`-linear retraction by
taking the average over `G` of the conjugates of `Ï€`.

## Implementation Notes
* These results assume `Invertible (Fintype.card G : k)` which is equivalent to the more
familiar `Â¬(ringChar k âˆ£ Fintype.card G)`. It is possible to convert between them using
`invertibleOfRingCharNotDvd` and `not_ringChar_dvd_of_invertible`.


## Future work
It's not so far to give the usual statement, that every finite dimensional representation
of a finite group is semisimple (i.e. a direct sum of irreducibles).
-/


universe u v w

noncomputable section

open Module MonoidAlgebra BigOperators

/-!
We now do the key calculation in Maschke's theorem.

Given `V â†’ W`, an inclusion of `k[G]` modules,
assume we have some retraction `Ï€` (i.e. `âˆ€ v, Ï€ (i v) = v`),
just as a `k`-linear map.
(When `k` is a field, this will be available cheaply, by choosing a basis.)

We now construct a retraction of the inclusion as a `k[G]`-linear map,
by the formula
$$ \frac{1}{|G|} \sum_{g \in G} gâ»Â¹ â€¢ Ï€(g â€¢ -). $$
-/

namespace LinearMap


-- At first we work with any `[CommRing k]`, and add the assumption that
-- `[Invertible (Fintype.card G : k)]` when it is required.
variable {k : Type u} [CommRing k] {G : Type u} [Group G]
variable {V : Type v} [AddCommGroup V] [Module k V] [Module (MonoidAlgebra k G) V]
variable [IsScalarTower k (MonoidAlgebra k G) V]
variable {W : Type w} [AddCommGroup W] [Module k W] [Module (MonoidAlgebra k G) W]
variable [IsScalarTower k (MonoidAlgebra k G) W]

variable (Ï€ : W â†’â‚—[k] V)

/-- We define the conjugate of `Ï€` by `g`, as a `k`-linear map. -/
def conjugate (g : G) : W â†’â‚—[k] V :=
  .comp (.comp (GroupSmul.linearMap k V gâ»Â¹) Ï€) (GroupSmul.linearMap k W g)
#align linear_map.conjugate LinearMap.conjugate

theorem conjugate_apply (g : G) (v : W) :
    Ï€.conjugate g v = MonoidAlgebra.single gâ»Â¹ (1 : k) â€¢ Ï€ (MonoidAlgebra.single g (1 : k) â€¢ v) :=
  rfl

variable (i : V â†’â‚—[MonoidAlgebra k G] W) (h : âˆ€ v : V, (Ï€ : W â†’ V) (i v) = v)

section

theorem conjugate_i (g : G) (v : V) : (conjugate Ï€ g : W â†’ V) (i v) = v := by
  rw [conjugate_apply, â† i.map_smul, h, â† mul_smul, single_mul_single, mul_one, mul_left_inv,
    â† one_def, one_smul]
#align linear_map.conjugate_i LinearMap.conjugate_i

end

variable (G) [Fintype G]

/-- The sum of the conjugates of `Ï€` by each element `g : G`, as a `k`-linear map.

(We postpone dividing by the size of the group as long as possible.)
-/
def sumOfConjugates : W â†’â‚—[k] V :=
  âˆ‘ g : G, Ï€.conjugate g
#align linear_map.sum_of_conjugates LinearMap.sumOfConjugates

lemma sumOfConjugates_apply (v : W) : Ï€.sumOfConjugates G v = âˆ‘ g : G, Ï€.conjugate g v :=
  LinearMap.sum_apply _ _ _

/-- In fact, the sum over `g : G` of the conjugate of `Ï€` by `g` is a `k[G]`-linear map.
-/
def sumOfConjugatesEquivariant : W â†’â‚—[MonoidAlgebra k G] V :=
  MonoidAlgebra.equivariantOfLinearOfComm (Ï€.sumOfConjugates G) fun g v => by
    simp only [sumOfConjugates_apply, Finset.smul_sum, conjugate_apply]
    -- âŠ¢ âˆ‘ x : G, MonoidAlgebra.single xâ»Â¹ 1 â€¢ â†‘Ï€ (MonoidAlgebra.single x 1 â€¢ MonoidA â€¦
    refine Fintype.sum_bijective (Â· * g) (Group.mulRight_bijective g) _ _ fun i â†¦ ?_
    -- âŠ¢ MonoidAlgebra.single iâ»Â¹ 1 â€¢ â†‘Ï€ (MonoidAlgebra.single i 1 â€¢ MonoidAlgebra.si â€¦
    simp only [smul_smul, single_mul_single, mul_inv_rev, mul_inv_cancel_left, one_mul]
    -- ğŸ‰ no goals
#align linear_map.sum_of_conjugates_equivariant LinearMap.sumOfConjugatesEquivariant

theorem sumOfConjugatesEquivariant_apply (v : W) :
    Ï€.sumOfConjugatesEquivariant G v = âˆ‘ g : G, Ï€.conjugate g v :=
  Ï€.sumOfConjugates_apply G v

section

variable [Invertible (Fintype.card G : k)]

/-- We construct our `k[G]`-linear retraction of `i` as
$$ \frac{1}{|G|} \sum_{g \in G} gâ»Â¹ â€¢ Ï€(g â€¢ -). $$
-/
def equivariantProjection : W â†’â‚—[MonoidAlgebra k G] V :=
  â…Ÿ(Fintype.card G : k) â€¢ Ï€.sumOfConjugatesEquivariant G
#align linear_map.equivariant_projection LinearMap.equivariantProjection

theorem equivariantProjection_apply (v : W) :
    Ï€.equivariantProjection G v = â…Ÿ(Fintype.card G : k) â€¢ âˆ‘ g : G, Ï€.conjugate g v := by
  simp only [equivariantProjection, smul_apply, sumOfConjugatesEquivariant_apply]
  -- ğŸ‰ no goals

theorem equivariantProjection_condition (v : V) : (Ï€.equivariantProjection G) (i v) = v := by
  rw [equivariantProjection_apply]
  -- âŠ¢ â…Ÿâ†‘(Fintype.card G) â€¢ âˆ‘ g : G, â†‘(conjugate Ï€ g) (â†‘i v) = v
  simp only [conjugate_i Ï€ i h]
  -- âŠ¢ â…Ÿâ†‘(Fintype.card G) â€¢ âˆ‘ x : G, v = v
  rw [Finset.sum_const, Finset.card_univ, nsmul_eq_smul_cast k, smul_smul,
    Invertible.invOf_mul_self, one_smul]
#align linear_map.equivariant_projection_condition LinearMap.equivariantProjection_condition

end

end LinearMap

end

namespace MonoidAlgebra

-- Now we work over a `[Field k]`.
variable {k : Type u} [Field k] {G : Type u} [Fintype G] [Invertible (Fintype.card G : k)]
variable [Group G]
variable {V : Type u} [AddCommGroup V] [Module k V] [Module (MonoidAlgebra k G) V]
variable [IsScalarTower k (MonoidAlgebra k G) V]
variable {W : Type u} [AddCommGroup W] [Module k W] [Module (MonoidAlgebra k G) W]
variable [IsScalarTower k (MonoidAlgebra k G) W]

theorem exists_leftInverse_of_injective (f : V â†’â‚—[MonoidAlgebra k G] W)
    (hf : LinearMap.ker f = âŠ¥) :
    âˆƒ g : W â†’â‚—[MonoidAlgebra k G] V, g.comp f = LinearMap.id := by
  obtain âŸ¨Ï†, hÏ†âŸ© := (f.restrictScalars k).exists_leftInverse_of_injective <| by
    simp only [hf, Submodule.restrictScalars_bot, LinearMap.ker_restrictScalars]
  refine âŸ¨Ï†.equivariantProjection G, FunLike.ext _ _ ?_âŸ©
  -- âŠ¢ âˆ€ (x : V), â†‘(LinearMap.comp (LinearMap.equivariantProjection G Ï†) f) x = â†‘Li â€¦
  exact Ï†.equivariantProjection_condition G _ <| FunLike.congr_fun hÏ†
  -- ğŸ‰ no goals
#align monoid_algebra.exists_left_inverse_of_injective MonoidAlgebra.exists_leftInverse_of_injective

namespace Submodule

theorem exists_isCompl (p : Submodule (MonoidAlgebra k G) V) :
    âˆƒ q : Submodule (MonoidAlgebra k G) V, IsCompl p q := by
  have : IsScalarTower k (MonoidAlgebra k G) p := p.isScalarTower'
  -- âŠ¢ âˆƒ q, IsCompl p q
  rcases MonoidAlgebra.exists_leftInverse_of_injective p.subtype p.ker_subtype with âŸ¨f, hfâŸ©
  -- âŠ¢ âˆƒ q, IsCompl p q
  refine âŸ¨LinearMap.ker f, LinearMap.isCompl_of_proj ?_âŸ©
  -- âŠ¢ âˆ€ (x : { x // x âˆˆ p }), â†‘f â†‘x = x
  exact FunLike.congr_fun hf
  -- ğŸ‰ no goals
#align monoid_algebra.submodule.exists_is_compl MonoidAlgebra.Submodule.exists_isCompl

/-- This also implies an instance `IsSemisimpleModule (MonoidAlgebra k G) V`. -/
instance complementedLattice : ComplementedLattice (Submodule (MonoidAlgebra k G) V) :=
  âŸ¨exists_isComplâŸ©
#align monoid_algebra.submodule.complemented_lattice MonoidAlgebra.Submodule.complementedLattice

end Submodule

end MonoidAlgebra
