/-
Copyright (c) 2022 Antoine Labelle. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Antoine Labelle
-/
import Mathlib.Algebra.Homology.ShortComplex.ModuleCat
import Mathlib.RepresentationTheory.Basic
import Mathlib.RepresentationTheory.FDRep

/-!
# Subspace of invariants a group representation

This file introduces the subspace of invariants of a group representation
and proves basic results about it.
The main tool used is the average of all elements of the group, seen as an element of
`MonoidAlgebra k G`. The action of this special element gives a projection onto the
subspace of invariants.
In order for the definition of the average element to make sense, we need to assume for most of the
results that the order of `G` is invertible in `k` (e. g. `k` has characteristic `0`).
-/

suppress_compilation

universe v u

open Representation

namespace GroupAlgebra

open MonoidAlgebra

variable (k G : Type*) [CommSemiring k] [Group G]
variable [Fintype G] [Invertible (Fintype.card G : k)]

/-- The average of all elements of the group `G`, considered as an element of `MonoidAlgebra k G`.
-/
noncomputable def average : MonoidAlgebra k G :=
  â…Ÿ (Fintype.card G : k) â€¢ âˆ‘ g : G, of k G g

/-- `average k G` is invariant under left multiplication by elements of `G`.
-/
@[simp]
theorem mul_average_left (g : G) : â†‘(Finsupp.single g 1) * average k G = average k G := by
  simp only [mul_one, Finset.mul_sum, Algebra.mul_smul_comm, average, MonoidAlgebra.of_apply,
    Finset.sum_congr, MonoidAlgebra.single_mul_single]
  set f : G â†’ MonoidAlgebra k G := fun x => Finsupp.single x 1
  show â…Ÿ (Fintype.card G : k) â€¢ âˆ‘ x : G, f (g * x) = â…Ÿ (Fintype.card G : k) â€¢ âˆ‘ x : G, f x
  rw [Function.Bijective.sum_comp (Group.mulLeft_bijective g) _]

/-- `average k G` is invariant under right multiplication by elements of `G`.
-/
@[simp]
theorem mul_average_right (g : G) : average k G * â†‘(Finsupp.single g 1) = average k G := by
  simp only [mul_one, Finset.sum_mul, Algebra.smul_mul_assoc, average, MonoidAlgebra.of_apply,
    Finset.sum_congr, MonoidAlgebra.single_mul_single]
  set f : G â†’ MonoidAlgebra k G := fun x => Finsupp.single x 1
  show â…Ÿ (Fintype.card G : k) â€¢ âˆ‘ x : G, f (x * g) = â…Ÿ (Fintype.card G : k) â€¢ âˆ‘ x : G, f x
  rw [Function.Bijective.sum_comp (Group.mulRight_bijective g) _]

end GroupAlgebra

namespace Representation

section Invariants

open GroupAlgebra

variable {k G V : Type*} [CommSemiring k] [Group G] [AddCommMonoid V] [Module k V]
variable (Ï : Representation k G V)

/-- The subspace of invariants, consisting of the vectors fixed by all elements of `G`.
-/
def invariants : Submodule k V where
  carrier := setOf fun v => âˆ€ g : G, Ï g v = v
  zero_mem' g := by simp only [map_zero]
  add_mem' hv hw g := by simp only [hv g, hw g, map_add]
  smul_mem' r v hv g := by simp only [hv g, LinearMap.map_smulâ‚›â‚—, RingHom.id_apply]

@[simp]
theorem mem_invariants (v : V) : v âˆˆ invariants Ï â†” âˆ€ g : G, Ï g v = v := by rfl

theorem invariants_eq_inter : (invariants Ï).carrier = â‹‚ g : G, Function.fixedPoints (Ï g) := by
  ext; simp [Function.IsFixedPt]

theorem invariants_eq_top [Ï.IsTrivial] :
    invariants Ï = âŠ¤ :=
eq_top_iff.2 (fun x _ g => by simp)

variable [Fintype G] [Invertible (Fintype.card G : k)]

/-- The action of `average k G` gives a projection map onto the subspace of invariants.
-/
@[simp]
noncomputable def averageMap : V â†’â‚—[k] V :=
  asAlgebraHom Ï (average k G)

/-- The `averageMap` sends elements of `V` to the subspace of invariants.
-/
theorem averageMap_invariant (v : V) : averageMap Ï v âˆˆ invariants Ï := fun g => by
  rw [averageMap, â† asAlgebraHom_single_one, â† LinearMap.mul_apply, â† map_mul (asAlgebraHom Ï),
    mul_average_left]

/-- The `averageMap` acts as the identity on the subspace of invariants.
-/
theorem averageMap_id (v : V) (hv : v âˆˆ invariants Ï) : averageMap Ï v = v := by
  rw [mem_invariants] at hv
  simp [average, map_sum, hv, Finset.card_univ, â† Nat.cast_smul_eq_nsmul k _ v, smul_smul]

theorem isProj_averageMap : LinearMap.IsProj Ï.invariants Ï.averageMap :=
  âŸ¨Ï.averageMap_invariant, Ï.averageMap_idâŸ©

end Invariants

namespace linHom

open CategoryTheory Action

section Rep

variable {k : Type u} [CommRing k] {G : Grp.{u}}

theorem mem_invariants_iff_comm {X Y : Rep k G} (f : X.V â†’â‚—[k] Y.V) (g : G) :
    (linHom X.Ï Y.Ï) g f = f â†” f.comp (X.Ï g) = (Y.Ï g).comp f := by
  dsimp
  rw [â† LinearMap.comp_assoc, â† ModuleCat.hom_ofHom (Y.Ï g), â† ModuleCat.hom_ofHom f,
      â† ModuleCat.hom_comp, â† ModuleCat.hom_ofHom (X.Ï gâ»Â¹), â† ModuleCat.hom_comp,
      Rep.ofHom_Ï, â† ÏAut_apply_inv X g, Rep.ofHom_Ï, â† ÏAut_apply_hom Y g, â† ModuleCat.hom_ext_iff,
      Iso.inv_comp_eq, ÏAut_apply_hom, â† ModuleCat.hom_ofHom (X.Ï g),
      â† ModuleCat.hom_comp, â† ModuleCat.hom_ext_iff]
  exact comm

/-- The invariants of the representation `linHom X.Ï Y.Ï` correspond to the representation
homomorphisms from `X` to `Y`. -/
@[simps]
def invariantsEquivRepHom (X Y : Rep k G) : (linHom X.Ï Y.Ï).invariants â‰ƒâ‚—[k] X âŸ¶ Y where
  toFun f := âŸ¨ModuleCat.ofHom f.val, fun g =>
    ModuleCat.hom_ext ((mem_invariants_iff_comm _ g).1 (f.property g))âŸ©
  map_add' _ _ := rfl
  map_smul' _ _ := rfl
  invFun f := âŸ¨f.hom.hom, fun g =>
    (mem_invariants_iff_comm _ g).2 (ModuleCat.hom_ext_iff.mp (f.comm g))âŸ©
  left_inv _ := by ext; rfl
  right_inv _ := by ext; rfl

end Rep

section FDRep

variable {k : Type u} [Field k] {G : Grp.{u}}

/-- The invariants of the representation `linHom X.Ï Y.Ï` correspond to the representation
homomorphisms from `X` to `Y`. -/
def invariantsEquivFDRepHom (X Y : FDRep k G) : (linHom X.Ï Y.Ï).invariants â‰ƒâ‚—[k] X âŸ¶ Y := by
  rw [â† FDRep.forgetâ‚‚_Ï, â† FDRep.forgetâ‚‚_Ï]
  -- Porting note: The original version used `linHom.invariantsEquivRepHom _ _ â‰ªâ‰«â‚—`
  exact linHom.invariantsEquivRepHom
    ((forgetâ‚‚ (FDRep k G) (Rep k G)).obj X) ((forgetâ‚‚ (FDRep k G) (Rep k G)).obj Y) â‰ªâ‰«â‚—
    FDRep.forgetâ‚‚HomLinearEquiv X Y

end FDRep

end linHom

section QuotientGroup

variable {k G V : Type*} [CommSemiring k] [Group G] [AddCommMonoid V] [Module k V]
variable (Ï : Representation k G V) (S : Subgroup G) [S.Normal]

/-- Given a normal subgroup `S â‰¤ G`, a `G`-representation `Ï` restricts to a `G`-representation on
the invariants of `Ï|_S`. -/
@[simps]
noncomputable def toInvariantsOfNormal :
    Representation k G (invariants (Ï.comp S.subtype)) where
  toFun g := ((Ï g).comp (Submodule.subtype _)).codRestrict _ (fun âŸ¨x, hxâŸ© âŸ¨s, hsâŸ© => by
    simpa using congr(Ï g $(hx âŸ¨(gâ»Â¹ * s * g), Subgroup.Normal.conj_mem' â€¹_â€º s hs gâŸ©)))
  map_one' := by ext; simp
  map_mul' _ _ := by ext; simp

instance : IsTrivial ((toInvariantsOfNormal Ï S).comp S.subtype) where
  out g := LinearMap.ext fun âŸ¨x, hxâŸ© => Subtype.ext <| by
    simpa using (hx g)

/-- Given a normal subgroup `S â‰¤ G`, a `G`-representation `Ï` induces a `G â§¸ S`-representation on
the invariants of `Ï|_S`. -/
noncomputable abbrev quotientGroupToInvariants :
    Representation k (G â§¸ S) (invariants (Ï.comp S.subtype)) :=
  ofQuotientGroup (toInvariantsOfNormal Ï S) S

end QuotientGroup
section Coinvariants

variable {k G V W : Type*} [CommRing k] [Group G] [AddCommGroup V] [Module k V]
variable [AddCommGroup W] [Module k W]
variable (Ï : Representation k G V) (S : Subgroup G)

/-- The submodule generated by elements of the form `Ï g x - x`. -/
abbrev augmentationSubmodule : Submodule k V :=
  Submodule.span k (Set.range <| fun (x : G Ã— V) => Ï x.1 x.2 - x.2)

variable {Ï}

lemma mem_augmentationSubmodule_of_eq (g : G) (x : V) (a : V) (h : Ï g x - x = a) :
    a âˆˆ augmentationSubmodule Ï :=
  Submodule.subset_span âŸ¨(g, x), hâŸ©

variable (Ï)

/-- The coinvariants of a representation, `V â§¸ âŸ¨{Ï g x - x | g âˆˆ G, x âˆˆ V}âŸ©`. -/
abbrev coinvariants := V â§¸ augmentationSubmodule Ï

/-- The quotient map from a representation to its coinvariants as a linear map. -/
abbrev coinvariantsMkQ := Submodule.mkQ (augmentationSubmodule Ï)

/-- A `G`-invariant linear map induces a linear map out of the coinvariants of a
`G`-representation. -/
def coinvariantsLift (f : V â†’â‚—[k] W) (h : âˆ€ (x : G), f âˆ˜â‚— Ï x = f) :
    Ï.coinvariants â†’â‚—[k] W :=
  Submodule.liftQ _ f <| Submodule.span_le.2 fun x âŸ¨âŸ¨g, yâŸ©, hyâŸ© => by
    simpa only [â† hy, SetLike.mem_coe, LinearMap.mem_ker, map_sub, sub_eq_zero, LinearMap.coe_comp,
      Function.comp_apply] using LinearMap.ext_iff.1 (h g) y

@[simp]
theorem coinvariantsLift_comp_mkQ (f : V â†’â‚—[k] W) (h : âˆ€ (x : G), f âˆ˜â‚— Ï x = f) :
  coinvariantsLift Ï f h âˆ˜â‚— (augmentationSubmodule Ï).mkQ = f := rfl

@[simp]
theorem coinvariantsLift_mk (f : V â†’â‚—[k] W) (h : âˆ€ (x : G), f âˆ˜â‚— Ï x = f) (x : V) :
  coinvariantsLift Ï f h (Submodule.Quotient.mk x) = f x := rfl

section Finsupp
open Finsupp

variable (Î± : Type*)

/-- Given a `G`-representation `(V, Ï)` and a type `Î±`, this is the map `(Î± â†’â‚€ V)_G â†’â‚— (Î± â†’â‚€ V_G)`
sending `âŸ¦single a vâŸ§ â†¦ single a âŸ¦vâŸ§`. -/
def coinvariantsToFinsupp :
    coinvariants (Ï.finsupp Î±) â†’â‚—[k] Î± â†’â‚€ coinvariants Ï :=
  coinvariantsLift _ (mapRange.linearMap (Submodule.mkQ _)) <| fun g => lhom_ext fun _ x => by
    simp [mapRange.linearMap, â† (Submodule.Quotient.eq _).2
      (mem_augmentationSubmodule_of_eq g x _ rfl), finsupp]

@[simp]
lemma coinvariantsToFinsupp_mk_single (x : Î±) (a : V) :
    coinvariantsToFinsupp Ï Î± (Submodule.Quotient.mk (single x a)) =
      single x (Submodule.Quotient.mk a) := by simp [coinvariantsToFinsupp]

/-- Given a `G`-representation `(V, Ï)` and a type `Î±`, this is the map `(Î± â†’â‚€ V_G) â†’â‚— (Î± â†’â‚€ V)_G`
sending `single a âŸ¦vâŸ§ â†¦ âŸ¦single a vâŸ§`. -/
def finsuppToCoinvariants :
    (Î± â†’â‚€ coinvariants Ï) â†’â‚—[k] coinvariants (Ï.finsupp Î±) :=
  lsum (R := k) k fun a => coinvariantsLift _ (Submodule.mkQ _ âˆ˜â‚— lsingle a) fun g =>
    LinearMap.ext fun x => (Submodule.Quotient.eq _).2 <|
    mem_augmentationSubmodule_of_eq g (single a x) _ <| by simp

@[simp]
lemma finsuppToCoinvariants_single_mk (a : Î±) (x : V) :
    finsuppToCoinvariants Ï Î± (single a <| Submodule.Quotient.mk x) =
      Submodule.Quotient.mk (single a x) := by simp [finsuppToCoinvariants]

/-- Given a `G`-representation `(V, Ï)` and a type `Î±`, this is the linear equivalence
`(Î± â†’â‚€ V)_G â‰ƒâ‚— (Î± â†’â‚€ V_G)` sending `âŸ¦single a vâŸ§ â†¦ single a âŸ¦vâŸ§`. -/
abbrev coinvariantsFinsuppLEquiv :
    coinvariants (Ï.finsupp Î±) â‰ƒâ‚—[k] Î± â†’â‚€ coinvariants Ï :=
  LinearEquiv.ofLinear (coinvariantsToFinsupp Ï Î±) (finsuppToCoinvariants Ï Î±)
    (lhom_ext fun _ x => Quotient.inductionOn' x fun _ => by
      simp [coinvariantsToFinsupp, finsuppToCoinvariants, Submodule.Quotient.mk''_eq_mk])
    (Submodule.linearMap_qext _ <| lhom_ext fun _ _ => by
      simp [finsuppToCoinvariants, coinvariantsToFinsupp])

end Finsupp

section TensorProduct
open TensorProduct

@[simp]
lemma coinvariants_mk_Ï_inv_tmul (Ï„ : Representation k G W) (x : V) (y : W) (g : G) :
    Submodule.Quotient.mk (p := (Ï.tprod Ï„).augmentationSubmodule) (Ï gâ»Â¹ x âŠ—â‚œ[k] y) =
      Submodule.Quotient.mk (p := (Ï.tprod Ï„).augmentationSubmodule) (x âŠ—â‚œ[k] Ï„ g y) :=
  (Submodule.Quotient.eq _).2 <| mem_augmentationSubmodule_of_eq gâ»Â¹ (x âŠ—â‚œ[k] Ï„ g y) _ <| by simp

@[simp]
lemma coinvariants_mk_tmul_Ï_inv (Ï„ : Representation k G W) (x : V) (y : W) (g : G) :
    Submodule.Quotient.mk (p := (Ï.tprod Ï„).augmentationSubmodule) (x âŠ—â‚œ[k] Ï„ gâ»Â¹ y) =
      Submodule.Quotient.mk (p := (Ï.tprod Ï„).augmentationSubmodule) (Ï g x âŠ—â‚œ[k] y) :=
  (Submodule.Quotient.eq _).2 <| mem_augmentationSubmodule_of_eq gâ»Â¹ (Ï g x âŠ—â‚œ[k] y) _ <| by simp

/-- Given a `k`-linear `G`-representation `V, Ï`, this is the map `(V âŠ— k[G])_G â†’â‚—[k] V` sending
`âŸ¦v âŠ— single g râŸ§ â†¦ r â€¢ Ï(gâ»Â¹)(v)`. -/
def ofCoinvariantsTprodLeftRegular :
    coinvariants (V := V âŠ—[k] (G â†’â‚€ k)) (Ï.tprod (leftRegular k G)) â†’â‚—[k] V :=
  coinvariantsLift _ (TensorProduct.lift (Finsupp.linearCombination _
    fun g => Ï gâ»Â¹) âˆ˜â‚— (TensorProduct.comm _ _ _).toLinearMap) fun _ => TensorProduct.ext <|
      LinearMap.ext fun _ => Finsupp.lhom_ext fun _ _ => by simp

@[simp]
lemma ofCoinvariantsTprodLeftRegular_mk_tmul_single (x : V) (g : G) (r : k) :
    ofCoinvariantsTprodLeftRegular Ï (Submodule.Quotient.mk (x âŠ—â‚œ Finsupp.single g r)) =
      r â€¢ Ï gâ»Â¹ x :=
  congr($(Finsupp.linearCombination_single k (v := fun g => Ï gâ»Â¹) r g) x)

/-- Given a `k`-linear `G`-representation `V, Ï`, this is the linear equivalence
`(V âŠ— k[G])_G â‰ƒâ‚—[k] V` sending `âŸ¦v âŠ— single g râŸ§ â†¦ r â€¢ Ï(gâ»Â¹)(v)`. -/
abbrev coinvariantsTprodLeftRegularLEquiv :
    coinvariants (V := V âŠ—[k] (G â†’â‚€ k)) (Ï.tprod (leftRegular k G)) â‰ƒâ‚—[k] V :=
  LinearEquiv.ofLinear (ofCoinvariantsTprodLeftRegular Ï)
    (Submodule.mkQ _ âˆ˜â‚— (mk k V (G â†’â‚€ k)).flip (Finsupp.single 1 1)) (by ext; simp) <|
    Submodule.linearMap_qext _ <| TensorProduct.ext <| LinearMap.ext fun a =>
      Finsupp.lhom_ext fun g r => (Submodule.Quotient.eq _).2 <| by
        apply mem_augmentationSubmodule_of_eq gâ»Â¹ (a âŠ—â‚œ Finsupp.single g r)
        simp_all [TensorProduct.smul_tmul', TensorProduct.smul_tmul]

end TensorProduct
end Coinvariants
section QuotientGroup

variable {k G V : Type*} [CommRing k] [Group G] [AddCommGroup V] [Module k V]
variable (Ï : Representation k G V) (S : Subgroup G)

lemma augmentationSubmodule_le_comap_Ï_of_normal [S.Normal] (g : G) :
    augmentationSubmodule (Ï.comp S.subtype) â‰¤
      (augmentationSubmodule <| Ï.comp S.subtype).comap (Ï g) :=
  Submodule.span_le.2 fun y âŸ¨âŸ¨s, xâŸ©, hsâŸ© => by
    simpa [â† hs] using mem_augmentationSubmodule_of_eq
      âŸ¨g * s * gâ»Â¹, Subgroup.Normal.conj_mem â€¹_â€º s.1 s.2 gâŸ© (Ï g x) _ <| by simp

/-- Given a normal subgroup `S â‰¤ G`, a `G`-representation `Ï` restricts to a `G`-representation on
the augmentation submodule of `Ï|_S`. -/
@[simps]
noncomputable def toAugmentationSubmoduleOfNormal [S.Normal] :
    Representation k G (augmentationSubmodule <| Ï.comp S.subtype) where
  toFun g := LinearMap.restrict (Ï g) <| augmentationSubmodule_le_comap_Ï_of_normal Ï S g
  map_one' := by ext; simp
  map_mul' _ _ := by ext; simp

/-- Given a normal subgroup `S â‰¤ G`, a `G`-representation `Ï` induces a `G`-representation on the
coinvariants of `Ï|_S`. -/
@[simps]
noncomputable def toCoinvariantsOfNormal [S.Normal] :
    Representation k G (coinvariants (Ï.comp S.subtype)) where
  toFun g := coinvariantsLift (Ï.comp S.subtype) ((augmentationSubmodule _).mkQ âˆ˜â‚— Ï g)
    fun âŸ¨s, hsâŸ© => by
      ext x
      simpa [Submodule.Quotient.eq] using mem_augmentationSubmodule_of_eq
        (Ï := Ï.comp S.subtype) âŸ¨g * s * gâ»Â¹, Subgroup.Normal.conj_mem â€¹_â€º s hs gâŸ© (Ï g x)
  map_one' := by ext; simp
  map_mul' _ _ := by ext; simp

instance [S.Normal] : IsTrivial ((toCoinvariantsOfNormal Ï S).comp S.subtype) where
  out g := Submodule.linearMap_qext _ <| by
    ext x
    simpa [Submodule.Quotient.eq] using mem_augmentationSubmodule_of_eq g x _ rfl

/-- Given a normal subgroup `S â‰¤ G`, a `G`-representation `Ï` induces a `G â§¸ S`-representation on
the coinvariants of `Ï|_S`. -/
noncomputable abbrev quotientGroupToCoinvariants [S.Normal] :
    Representation k (G â§¸ S) (coinvariants (Ï.comp S.subtype)) :=
  ofQuotientGroup (toCoinvariantsOfNormal Ï S) S

end QuotientGroup
end Representation

namespace Rep

open CategoryTheory

variable (k G : Type u) [CommRing k] [Group G] (A : Rep k G)

section Invariants

/-- The functor sending a representation to its submodule of invariants. -/
@[simps]
noncomputable def invariantsFunctor : Rep k G â¥¤ ModuleCat.{u} k where
  obj A := ModuleCat.of k A.Ï.invariants
  map {A B} f := ModuleCat.ofHom <| (f.hom.hom âˆ˜â‚— A.Ï.invariants.subtype).codRestrict
    B.Ï.invariants fun âŸ¨c, hcâŸ© g => by
      have := (hom_comm_apply f g c).symm
      simp_all [hc g]

instance : (invariantsFunctor k G).PreservesZeroMorphisms where

instance : (invariantsFunctor k G).Additive where

/-- The adjunction between the functor equipping a module with the trivial representation, and
the functor sending a representation to its submodule of invariants. -/
noncomputable abbrev invariantsAdjunction : trivialFunctor âŠ£ invariantsFunctor k G :=
  Adjunction.mkOfHomEquiv {
    homEquiv := fun _ _ => {
      toFun := fun f => ModuleCat.ofHom <|
        LinearMap.codRestrict _ f.hom.hom fun x g => (hom_comm_apply f _ _).symm
      invFun := fun f => {
        hom := ModuleCat.ofHom (Submodule.subtype _ âˆ˜â‚— f.hom)
        comm := fun g => by ext x; exact ((f x).2 g).symm }
      left_inv := by intro; rfl
      right_inv := by intro; rfl }
    homEquiv_naturality_left_symm := by intros; rfl
    homEquiv_naturality_right := by intros; rfl }

noncomputable instance : Limits.PreservesLimits (invariantsFunctor k G) :=
  (invariantsAdjunction k G).rightAdjoint_preservesLimits

variable {k G}

/-- Given a normal subgroup `S â‰¤ G`, a `G`-representation `Ï` induces a `G â§¸ S`-representation on
the invariants of `Ï|_S`. -/
abbrev quotientGroupToInvariants (S : Subgroup G) [S.Normal] :=
  Rep.of (A.Ï.quotientGroupToInvariants S)

end Invariants
section Coinvariants

variable {k G A} {B C : Rep k G} {n : â„•} {V : Type u} [AddCommGroup V] [Module k V]

open Representation

/-- The linear map underlying a `G`-representation morphism `A âŸ¶ V`, where `V` has the trivial
representation, factors through `A_G`. -/
abbrev coinvariantsLift (f : A âŸ¶ Rep.trivial k G V) :
    coinvariants A.Ï â†’â‚—[k] V :=
  Representation.coinvariantsLift _ f.hom.hom fun _ => congr(ModuleCat.Hom.hom $(f.comm _))

/-- A `G`-representation morphism `A âŸ¶ B` induces a linear map `A_G â†’â‚—[k] B_G`. -/
abbrev coinvariantsMap (f : A âŸ¶ B) :
    coinvariants A.Ï â†’â‚—[k] coinvariants B.Ï :=
  Representation.coinvariantsLift _ (Submodule.mkQ _ âˆ˜â‚— f.hom.hom) fun g => LinearMap.ext fun x =>
    (Submodule.Quotient.eq _).2 <| mem_augmentationSubmodule_of_eq g (f.hom x) _ <| by
      simpa using (hom_comm_apply f g x).symm

@[simp]
theorem coinvariantsMap_comp_mkQ (f : A âŸ¶ B) :
    coinvariantsMap f âˆ˜â‚— coinvariantsMkQ A.Ï = coinvariantsMkQ B.Ï âˆ˜â‚— f.hom.hom := rfl

@[simp]
theorem coinvariantsMap_mk (f : A âŸ¶ B) (x : A) :
    coinvariantsMap f (Submodule.Quotient.mk x) = Submodule.Quotient.mk (f.hom x) := rfl

@[simp]
theorem coinvariantsMap_id (A : Rep k G) :
    coinvariantsMap (ğŸ™ A) = LinearMap.id := by
  ext; rfl

@[simp]
theorem coinvariantsMap_comp (f : A âŸ¶ B) (g : B âŸ¶ C) :
    coinvariantsMap (f â‰« g) = coinvariantsMap g âˆ˜â‚— coinvariantsMap f := by
  ext; rfl

variable (A B)

/-- For a representation `A` of a finite group `G`, the norm map `A âŸ¶ A` induces a linear map
`A_G â†’â‚—[k] Aá´³`. -/
noncomputable def liftRestrictNorm [Fintype G] :
    A.Ï.coinvariants â†’â‚—[k] A.Ï.invariants :=
  A.Ï.coinvariantsLift ((norm A).hom.hom.codRestrict _ fun a g =>
      congr($(Ï_comp_norm_hom_hom A g) a))
    fun g => LinearMap.ext fun x => Subtype.ext <| congr($(norm_hom_hom_comp_Ï A g) x)

variable (k G)

/-- The functor sending a representation to its coinvariants. -/
@[simps]
def coinvariantsFunctor : Rep k G â¥¤ ModuleCat k where
  obj A := ModuleCat.of k (A.Ï.coinvariants)
  map f := ModuleCat.ofHom (coinvariantsMap f)

instance : (coinvariantsFunctor k G).Additive where
  map_add := ModuleCat.hom_ext <| LinearMap.ext fun x => Quotient.inductionOn' x (fun _ => rfl)

/-- The adjunction between the functor sending a representation to its coinvariants and the functor
equipping a module with the trivial representation. -/
noncomputable def coinvariantsAdjunction : coinvariantsFunctor k G âŠ£ trivialFunctor :=
  Adjunction.mkOfHomEquiv {
    homEquiv := fun X Y => {
      toFun := fun f => {
        hom := ModuleCat.ofHom (f.hom âˆ˜â‚— X.Ï.augmentationSubmodule.mkQ)
        comm := fun g => by
          ext x
          exact congr(f.hom $((Submodule.Quotient.eq <| X.Ï.augmentationSubmodule).2
            (X.Ï.mem_augmentationSubmodule_of_eq g x _ rfl))) }
      invFun := fun f => ModuleCat.ofHom (coinvariantsLift f)
      left_inv := fun _ => ModuleCat.hom_ext <| Submodule.linearMap_qext _ rfl
      right_inv := fun _ => Action.Hom.ext <| rfl }
    homEquiv_naturality_left_symm := fun _ _ => ModuleCat.hom_ext <| Submodule.linearMap_qext _ rfl
    homEquiv_naturality_right := by intros; rfl }

instance : (coinvariantsFunctor k G).PreservesZeroMorphisms where
  map_zero _ _ := ModuleCat.hom_ext <| Submodule.linearMap_qext _ rfl

instance : Limits.PreservesColimits (coinvariantsFunctor k G) :=
  (coinvariantsAdjunction k G).leftAdjoint_preservesColimits

variable {k G} (S : Subgroup G) [S.Normal]

/-- Given a normal subgroup `S â‰¤ G`, a `G`-representation `Ï` restricts to a `G`-representation on
the augmentation submodule of `Ï|_S`. -/
abbrev toAugmentationSubmoduleOfNormal :=
  Rep.of (A.Ï.toAugmentationSubmoduleOfNormal S)

/-- Given a normal subgroup `S â‰¤ G`, a `G`-representation `Ï` induces a `G`-representation on the
coinvariants of `Ï|_S`. -/
abbrev toCoinvariantsOfNormal :=
  Rep.of (A.Ï.toCoinvariantsOfNormal S)

/-- Given a normal subgroup `S â‰¤ G`, a `G`-representation `A` induces a short exact sequence of
`G`-representations `0 âŸ¶ (I_S)A âŸ¶ A âŸ¶ A_S âŸ¶ 0` where `(I_S)A` is the submodule of `A`
generated by elements of the form `Ï(s)(x) - x` for `s âˆˆ S, x âˆˆ A`. -/
@[simps]
def coinvariantsShortComplex : ShortComplex (Rep k G) where
  Xâ‚ := toAugmentationSubmoduleOfNormal A S
  Xâ‚‚ := A
  Xâ‚ƒ := toCoinvariantsOfNormal A S
  f := âŸ¨ModuleCat.ofHom (Submodule.subtype _), fun _ => rflâŸ©
  g := âŸ¨ModuleCat.ofHom (Submodule.mkQ _), fun _ => rflâŸ©
  zero := by ext x; exact (Submodule.Quotient.mk_eq_zero _).2 x.2

lemma coinvariantsShortComplex_shortExact : (coinvariantsShortComplex A S).ShortExact where
  exact := (forgetâ‚‚ _ (ModuleCat k)).reflects_exact_of_faithful _ <|
    (ShortComplex.moduleCat_exact_iff _).2
      fun x hx => âŸ¨(âŸ¨x, (Submodule.Quotient.mk_eq_zero _).1 hxâŸ© :
      augmentationSubmodule <| A.Ï.comp S.subtype), rflâŸ©
  mono_f := (Rep.mono_iff_injective _).2 fun _ _ h => Subtype.ext h
  epi_g := (Rep.epi_iff_surjective _).2 <| Submodule.mkQ_surjective _

instance [S.Normal] : IsTrivial ((A.toCoinvariantsOfNormal S).Ï.comp S.subtype) where
  out g := Submodule.linearMap_qext _ <| by ext; simp

/-- Given a normal subgroup `S â‰¤ G`, a `G`-representation `Ï` induces a `G â§¸ S`-representation on
the coinvariants of `Ï|_S`. -/
abbrev quotientGroupToCoinvariants :=
  Rep.of (A.Ï.quotientGroupToCoinvariants S)

variable (Î± : Type u) [DecidableEq Î±]

open MonoidalCategory Finsupp

/-- Given a `k`-linear `G`-representation `(A, Ï)` and a type `Î±`, this is the map
`(A âŠ— (Î± â†’â‚€ k[G]))_G â†’â‚—[k] (Î± â†’â‚€ A)` sending
`âŸ¦a âŠ— single x (single g r)âŸ§ â†¦ single x (r â€¢ Ï(gâ»Â¹)(a)).` -/
def coinvariantsTensorFreeToFinsupp :
    (A âŠ— free k G Î±).Ï.coinvariants â†’â‚—[k] (Î± â†’â‚€ A) :=
  (coinvariantsFinsuppLEquiv _ Î± â‰ªâ‰«â‚— lcongr (Equiv.refl Î±)
    (coinvariantsTprodLeftRegularLEquiv A.Ï)).toLinearMap âˆ˜â‚— coinvariantsMap (finsuppTensorRight A
      (leftRegular k G) Î±).hom

variable {A Î±}

@[simp]
lemma coinvariantsTensorFreeToFinsupp_mk_tmul_single (x : A) (i : Î±) (g : G) (r : k) :
    coinvariantsTensorFreeToFinsupp A Î± (Submodule.Quotient.mk (x âŠ—â‚œ single i (single g r))) =
      single i (r â€¢ A.Ï gâ»Â¹ x) := by
  simp [coinvariantsTensorFreeToFinsupp, coinvariantsMap,
    finsuppTensorRight, TensorProduct.finsuppRight]

variable (A Î±)

/-- Given a `k`-linear `G`-representation `(A, Ï)` and a type `Î±`, this is the map
`(Î± â†’â‚€ A) â†’â‚—[k] (A âŠ— (Î± â†’â‚€ k[G]))_G` sending `single x a â†¦ âŸ¦a âŠ—â‚œ single x 1âŸ§.` -/
def finsuppToCoinvariantsTensorFree :
    (Î± â†’â‚€ A) â†’â‚—[k] coinvariants (A âŠ— (free k G Î±)).Ï :=
  coinvariantsMap ((finsuppTensorRight A (leftRegular k G) Î±)).inv âˆ˜â‚—
    (coinvariantsFinsuppLEquiv _ Î± â‰ªâ‰«â‚—
    lcongr (Equiv.refl Î±) (coinvariantsTprodLeftRegularLEquiv A.Ï)).symm.toLinearMap

variable {A Î±}

lemma finsuppToCoinvariantsTensorFree_single (i : Î±) (x : A) :
    finsuppToCoinvariantsTensorFree A Î± (single i x) =
      Submodule.Quotient.mk (x âŠ—â‚œ single i (single (1 : G) (1 : k))) := by
  have := finsuppTensorRight_inv_hom (A := A) (B := leftRegular k G)
  simp_all [finsuppToCoinvariantsTensorFree, coinvariantsMap,
    ModuleCat.MonoidalCategory.instMonoidalCategoryStruct_tensorObj,
    ModuleCat.MonoidalCategory.tensorObj]

variable (A Î±)

/-- Given a `k`-linear `G`-representation `(A, Ï)` and a type `Î±`, this is the linear equivalence
`(A âŠ— (Î± â†’â‚€ k[G]))_G â‰ƒâ‚—[k] (Î± â†’â‚€ A)` sending
`âŸ¦a âŠ— single x (single g r)âŸ§ â†¦ single x (r â€¢ Ï(gâ»Â¹)(a)).` -/
abbrev coinvariantsTensorFreeLEquiv :
    coinvariants (A âŠ— free k G Î±).Ï â‰ƒâ‚—[k] (Î± â†’â‚€ A) :=
  LinearEquiv.ofLinear (coinvariantsTensorFreeToFinsupp A Î±) (finsuppToCoinvariantsTensorFree A Î±)
    (lhom_ext fun _ _ => by
      rw [LinearMap.comp_apply, finsuppToCoinvariantsTensorFree_single,
        coinvariantsTensorFreeToFinsupp_mk_tmul_single]
      simp) <|
    Submodule.linearMap_qext _ <| TensorProduct.ext <| LinearMap.ext fun a => lhom_ext' fun i =>
      lhom_ext fun g r => by
        have := coinvariantsTensorFreeToFinsupp_mk_tmul_single a i g r
        have := finsuppToCoinvariantsTensorFree_single (A := A) i
        simp_all [Submodule.Quotient.eq, TensorProduct.smul_tmul]

variable (k G) in
/-- The functor sending `A, B` to `(A âŠ—[k] B)_G`. This is naturally isomorphic to the functor
sending `A, B` to `A âŠ—[k[G]] B`, where we give `A` the `k[G]áµáµ’áµ–`-module structure defined by
`g â€¢ a := A.Ï gâ»Â¹ a`. -/
@[simps]
def coinvariantsTensor : Rep k G â¥¤ Rep k G â¥¤ ModuleCat k where
  obj A := MonoidalCategory.tensorLeft A â‹™ coinvariantsFunctor k G
  map f := {
    app := fun A => ModuleCat.ofHom (coinvariantsMap (f âŠ— ğŸ™ A))
    naturality := fun _ _ _ => ModuleCat.hom_ext <| Submodule.linearMap_qext _ <|
      TensorProduct.ext' fun _ _ => by rfl }
  map_id _ := NatTrans.ext <| funext fun _ => by
    simpa only [tensorHom_id, id_whiskerRight] using (coinvariantsFunctor k G).map_id _
  map_comp _ _ := NatTrans.ext <| funext fun _ => by
    simpa only [tensorHom_id, comp_whiskerRight] using (coinvariantsFunctor k G).map_comp _ _

instance (A : Rep k G) : ((coinvariantsTensor k G).obj A).Additive := by
  unfold coinvariantsTensor
  infer_instance

end Coinvariants

/-- Given a finite group `G`, this is the natural transformation sending a `G`-representation `A`
to the map `A_G â†’â‚—[k] Aá´³` induced by the norm map on `A`. -/
@[simps]
noncomputable def liftRestrictNormNatTrans [Fintype G] :
    coinvariantsFunctor k G âŸ¶ invariantsFunctor k G where
  app A := ModuleCat.ofHom <| liftRestrictNorm A
  naturality _ _ f := ModuleCat.hom_ext <| Submodule.linearMap_qext _ <|
    LinearMap.ext fun _ => Subtype.ext <| by
      have := hom_comm_apply f
      simp_all [norm, liftRestrictNorm]

end Rep
