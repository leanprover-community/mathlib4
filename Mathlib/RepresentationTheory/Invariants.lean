/-
Copyright (c) 2022 Antoine Labelle. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Antoine Labelle
-/
import Mathlib.RepresentationTheory.Basic
import Mathlib.RepresentationTheory.FDRep

/-!
# Subspace of invariants a group representation

This file introduces the subspace of invariants of a group representation
and proves basic results about it.
The main tool used is the average of all elements of the group, seen as an element of
`MonoidAlgebra k G`. The action of this special element gives a projection onto the
subspace of invariants.
In order for the definition of the average element to make sense, we need to assume for most of the
results that the order of `G` is invertible in `k` (e. g. `k` has characteristic `0`).
-/

suppress_compilation

universe v u

open MonoidAlgebra

open Representation

namespace GroupAlgebra

variable (k G : Type*) [CommSemiring k] [Group G]
variable [Fintype G] [Invertible (Fintype.card G : k)]

/-- The average of all elements of the group `G`, considered as an element of `MonoidAlgebra k G`.
-/
noncomputable def average : MonoidAlgebra k G :=
  â…Ÿ (Fintype.card G : k) â€¢ âˆ‘ g : G, of k G g

/-- `average k G` is invariant under left multiplication by elements of `G`.
-/
@[simp]
theorem mul_average_left (g : G) : â†‘(Finsupp.single g 1) * average k G = average k G := by
  simp only [mul_one, Finset.mul_sum, Algebra.mul_smul_comm, average, MonoidAlgebra.of_apply,
    Finset.sum_congr, MonoidAlgebra.single_mul_single]
  set f : G â†’ MonoidAlgebra k G := fun x => Finsupp.single x 1
  show â…Ÿ (Fintype.card G : k) â€¢ âˆ‘ x : G, f (g * x) = â…Ÿ (Fintype.card G : k) â€¢ âˆ‘ x : G, f x
  rw [Function.Bijective.sum_comp (Group.mulLeft_bijective g) _]

/-- `average k G` is invariant under right multiplication by elements of `G`.
-/
@[simp]
theorem mul_average_right (g : G) : average k G * â†‘(Finsupp.single g 1) = average k G := by
  simp only [mul_one, Finset.sum_mul, Algebra.smul_mul_assoc, average, MonoidAlgebra.of_apply,
    Finset.sum_congr, MonoidAlgebra.single_mul_single]
  set f : G â†’ MonoidAlgebra k G := fun x => Finsupp.single x 1
  show â…Ÿ (Fintype.card G : k) â€¢ âˆ‘ x : G, f (x * g) = â…Ÿ (Fintype.card G : k) â€¢ âˆ‘ x : G, f x
  rw [Function.Bijective.sum_comp (Group.mulRight_bijective g) _]

end GroupAlgebra

namespace Representation

section Invariants

open GroupAlgebra

variable {k G V : Type*} [CommSemiring k] [Group G] [AddCommMonoid V] [Module k V]
variable (Ï : Representation k G V)

/-- The subspace of invariants, consisting of the vectors fixed by all elements of `G`.
-/
def invariants : Submodule k V where
  carrier := setOf fun v => âˆ€ g : G, Ï g v = v
  zero_mem' g := by simp only [map_zero]
  add_mem' hv hw g := by simp only [hv g, hw g, map_add]
  smul_mem' r v hv g := by simp only [hv g, LinearMap.map_smulâ‚›â‚—, RingHom.id_apply]

@[simp]
theorem mem_invariants (v : V) : v âˆˆ invariants Ï â†” âˆ€ g : G, Ï g v = v := by rfl

theorem invariants_eq_inter : (invariants Ï).carrier = â‹‚ g : G, Function.fixedPoints (Ï g) := by
  ext; simp [Function.IsFixedPt]

theorem invariants_eq_top [Ï.IsTrivial] :
    invariants Ï = âŠ¤ :=
eq_top_iff.2 (fun x _ g => Ï.apply_eq_self g x)

variable [Fintype G] [Invertible (Fintype.card G : k)]

/-- The action of `average k G` gives a projection map onto the subspace of invariants.
-/
@[simp]
noncomputable def averageMap : V â†’â‚—[k] V :=
  asAlgebraHom Ï (average k G)

/-- The `averageMap` sends elements of `V` to the subspace of invariants.
-/
theorem averageMap_invariant (v : V) : averageMap Ï v âˆˆ invariants Ï := fun g => by
  rw [averageMap, â† asAlgebraHom_single_one, â† LinearMap.mul_apply, â† map_mul (asAlgebraHom Ï),
    mul_average_left]

/-- The `averageMap` acts as the identity on the subspace of invariants.
-/
theorem averageMap_id (v : V) (hv : v âˆˆ invariants Ï) : averageMap Ï v = v := by
  rw [mem_invariants] at hv
  simp [average, map_sum, hv, Finset.card_univ, â† Nat.cast_smul_eq_nsmul k _ v, smul_smul]

theorem isProj_averageMap : LinearMap.IsProj Ï.invariants Ï.averageMap :=
  âŸ¨Ï.averageMap_invariant, Ï.averageMap_idâŸ©

end Invariants

namespace linHom

open CategoryTheory Action

section Rep

variable {k : Type u} [CommRing k] {G : Grp.{u}}

theorem mem_invariants_iff_comm {X Y : Rep k G} (f : X.V â†’â‚—[k] Y.V) (g : G) :
    (linHom X.Ï Y.Ï) g f = f â†” f.comp (X.Ï g) = (Y.Ï g).comp f := by
  dsimp
  erw [â† ÏAut_apply_inv]
  rw [â† LinearMap.comp_assoc, â† ModuleCat.comp_def, â† ModuleCat.comp_def, Iso.inv_comp_eq,
    ÏAut_apply_hom]
  exact comm

/-- The invariants of the representation `linHom X.Ï Y.Ï` correspond to the representation
homomorphisms from `X` to `Y`. -/
@[simps]
def invariantsEquivRepHom (X Y : Rep k G) : (linHom X.Ï Y.Ï).invariants â‰ƒâ‚—[k] X âŸ¶ Y where
  toFun f := âŸ¨f.val, fun g => (mem_invariants_iff_comm _ g).1 (f.property g)âŸ©
  map_add' _ _ := rfl
  map_smul' _ _ := rfl
  invFun f := âŸ¨f.hom, fun g => (mem_invariants_iff_comm _ g).2 (f.comm g)âŸ©
  left_inv _ := by ext; rfl
  right_inv _ := by ext; rfl

end Rep

section FDRep

variable {k : Type u} [Field k] {G : Grp.{u}}

/-- The invariants of the representation `linHom X.Ï Y.Ï` correspond to the representation
homomorphisms from `X` to `Y`. -/
def invariantsEquivFDRepHom (X Y : FDRep k G) : (linHom X.Ï Y.Ï).invariants â‰ƒâ‚—[k] X âŸ¶ Y := by
  rw [â† FDRep.forgetâ‚‚_Ï, â† FDRep.forgetâ‚‚_Ï]
  -- Porting note: The original version used `linHom.invariantsEquivRepHom _ _ â‰ªâ‰«â‚—`
  exact linHom.invariantsEquivRepHom
    ((forgetâ‚‚ (FDRep k G) (Rep k G)).obj X) ((forgetâ‚‚ (FDRep k G) (Rep k G)).obj Y) â‰ªâ‰«â‚—
    FDRep.forgetâ‚‚HomLinearEquiv X Y

end FDRep

end linHom

section Inf

variable {k G V : Type*} [CommSemiring k] [Group G] [AddCommMonoid V] [Module k V]
variable (Ï : Representation k G V) (S : Subgroup G)

/-- Given a normal subgroup `S â‰¤ G`, a `G`-representation `Ï` restricts to a `G`-representation on
the invariants of `Ï|_S`. -/
@[simps]
noncomputable def invariantsOfNormal [S.Normal] :
    Representation k G (invariants (Ï.comp S.subtype)) where
  toFun g := ((Ï g).comp (Submodule.subtype _)).codRestrict _ (fun âŸ¨x, hxâŸ© âŸ¨s, hsâŸ© => by
    simpa using congr(Ï g $(hx âŸ¨(gâ»Â¹ * s * g), Subgroup.Normal.conj_mem' â€¹_â€º s hs gâŸ©)))
  map_one' := by ext; simp
  map_mul' _ _ := by ext; simp

/-- Given a normal subgroup `S â‰¤ G`, a `G`-representation `Ï` induces a `G â§¸ S`-representation on
the invariants of `Ï|_S`. -/
noncomputable def inf [S.Normal] : Representation k (G â§¸ S) (invariants (Ï.comp S.subtype)) :=
  (QuotientGroup.con S).lift (invariantsOfNormal Ï S)
    fun x y âŸ¨âŸ¨z, hzâŸ©, hâŸ© => LinearMap.ext fun âŸ¨w, hwâŸ© => Subtype.ext <| by
    simpa [â† h] using congr(Ï y $(hw âŸ¨z.unop, hzâŸ©))

variable {Ï S} in
@[simp]
lemma inf_apply [S.Normal] (g : G) (x : invariants (Ï.comp S.subtype)) :
    (inf Ï S (g : G â§¸ S) x).1 = Ï g x :=
  rfl

end Inf
section Coinvariants

variable {k G V W : Type*} [CommRing k] [Group G] [AddCommGroup V] [Module k V]
variable [AddCommGroup W] [Module k W]
variable (Ï : Representation k G V) (S : Subgroup G)

/-- The submodule generated by elements of the form `Ï g x - x`. -/
abbrev coinvariantsKer : Submodule k V :=
  Submodule.span k (Set.range <| fun (x : G Ã— V) => Ï x.1 x.2 - x.2)

lemma mem_coinvariantsKer_of_eq (g : G) (x : V) (a : V) (h : Ï g x - x = a) :
    a âˆˆ coinvariantsKer Ï :=
  Submodule.subset_span âŸ¨(g, x), hâŸ©

/-- The coinvariants of a representation, `V â§¸ âŸ¨{Ï g x - x | g âˆˆ G, x âˆˆ V}âŸ©`. -/
abbrev coinvariants := V â§¸ coinvariantsKer Ï

/-- A `G`-invariant linear map induces a linear map out of the coinvariants of a
`G`-representation. -/
def coinvariantsLift (f : V â†’â‚—[k] W) (h : âˆ€ (x : G), f âˆ˜â‚— Ï x = f) :
    Ï.coinvariants â†’â‚—[k] W :=
  Submodule.liftQ _ f <| Submodule.span_le.2 fun x âŸ¨âŸ¨g, yâŸ©, hyâŸ© => by
    simpa only [â† hy, SetLike.mem_coe, LinearMap.mem_ker, map_sub, sub_eq_zero, LinearMap.coe_comp,
      Function.comp_apply] using LinearMap.ext_iff.1 (h g) y

@[simp]
theorem coinvariantsLift_mkQ (f : V â†’â‚—[k] W) (h : âˆ€ (x : G), f âˆ˜â‚— Ï x = f) :
  coinvariantsLift Ï f h âˆ˜â‚— (coinvariantsKer Ï).mkQ = f := rfl

@[simp]
theorem coinvariantsLift_apply (f : V â†’â‚—[k] W) (h : âˆ€ (x : G), f âˆ˜â‚— Ï x = f) (x : V) :
  coinvariantsLift Ï f h (Submodule.Quotient.mk x) = f x := rfl

end Coinvariants
section Coinf

variable {k G V : Type*} [CommRing k] [Group G] [AddCommGroup V] [Module k V]
variable (Ï : Representation k G V) (S : Subgroup G)

/-- Given a normal subgroup `S â‰¤ G`, a `G`-representation `Ï` induces a `G`-representation on the
coinvariants of `Ï|_S`. -/
@[simps]
noncomputable def coinvariantsOfNormal [S.Normal] :
    Representation k G (coinvariants (Ï.comp S.subtype)) where
  toFun g := coinvariantsLift (Ï.comp S.subtype) ((coinvariantsKer _).mkQ âˆ˜â‚— Ï g) fun âŸ¨s, hsâŸ© => by
    ext x
    simpa [Submodule.Quotient.eq] using mem_coinvariantsKer_of_eq
      (Ï.comp S.subtype) âŸ¨g * s * gâ»Â¹, Subgroup.Normal.conj_mem â€¹_â€º s hs gâŸ© (Ï g x)
  map_one' := by ext; simp
  map_mul' _ _ := by ext; simp

/-- Given a normal subgroup `S â‰¤ G`, a `G`-representation `Ï` induces a `G â§¸ S`-representation on
the coinvariants of `Ï|_S`. -/
noncomputable def coinf [h1 : S.Normal] :
    Representation k (G â§¸ S) (coinvariants (Ï.comp S.subtype)) :=
  (QuotientGroup.con S).lift (coinvariantsOfNormal Ï S)
    fun x y âŸ¨âŸ¨z, hzâŸ©, hâŸ© => Submodule.linearMap_qext _ <| by
      ext w
      simpa [â† h, Submodule.Quotient.eq] using mem_coinvariantsKer_of_eq _
        âŸ¨y * z.unop * y â»Â¹, h1.conj_mem z.unop hz yâŸ© (Ï y w) _ (by simp)

variable {Ï S} in
@[simp]
lemma coinf_apply [S.Normal] (g : G) (x : V) :
    coinf Ï S (g : G â§¸ S) (Submodule.Quotient.mk x) = Submodule.Quotient.mk (Ï g x) :=
  rfl

end Coinf
end Representation
namespace Rep

open CategoryTheory

variable (k G : Type u) [CommRing k] [Group G] (A : Rep k G)

section Invariants

/-- The functor sending a representation to its submodule of invariants. -/
@[simps]
noncomputable def invariantsFunctor : Rep k G â¥¤ ModuleCat.{u} k where
  obj A := ModuleCat.of k A.Ï.invariants
  map {A B} f := (hom f âˆ˜â‚— A.Ï.invariants.subtype).codRestrict
    B.Ï.invariants fun âŸ¨c, hcâŸ© g => by simp [â† hom_comm_apply'', hc g]

instance : (invariantsFunctor k G).PreservesZeroMorphisms where

instance : (invariantsFunctor k G).Additive where

/-- The adjunction between the functor equipping a module with the trivial representation, and
the functor sending a representation to its submodule of invariants. -/
noncomputable abbrev invariantsAdjunction : trivialFunctor âŠ£ invariantsFunctor k G :=
  Adjunction.mkOfHomEquiv {
    homEquiv := fun X Y => {
      toFun := fun f => LinearMap.codRestrict _ f.hom fun x g =>
        (hom_comm_apply'' f _ _).symm
      invFun := fun f => {
        hom := Submodule.subtype _ âˆ˜â‚— f
        comm := fun g => by ext x; exact ((f x).2 g).symm }
      left_inv := by intros f; rfl
      right_inv := by intros f; rfl }
    homEquiv_naturality_left_symm := by intros; rfl
    homEquiv_naturality_right := by intros; rfl }

noncomputable instance : Limits.PreservesLimits (invariantsFunctor k G) :=
  (invariantsAdjunction k G).rightAdjointPreservesLimits

end Invariants
section Inf

variable {k G} (S : Subgroup G) [S.Normal]

/-- Given a normal subgroup `S â‰¤ G`, a `G`-representation `Ï` induces a `G â§¸ S`-representation on
the invariants of `Ï|_S`. -/
abbrev inf := Rep.of (A.Ï.inf S)

/-- Given a normal subgroup `S â‰¤ G`, a `G`-representation morphism `f : A âŸ¶ B` induces a
`G â§¸ S`-representation morphism `invariants Ï_A|_S âŸ¶ invariants Ï_B|_S`. -/
@[simps]
noncomputable def infMap {A B : Rep k G} (Ï† : A âŸ¶ B) :
    inf A S âŸ¶ inf B S where
  hom := (invariantsFunctor k S).map ((Action.res _ S.subtype).map Ï†)
  comm g := QuotientGroup.induction_on g (fun g => LinearMap.ext
    fun x => Subtype.ext (Rep.hom_comm_apply'' Ï† g x.1))

/-- Given a normal subgroup `S â‰¤ G`, this functor sends a `G`-representation `Ï` to the
`G â§¸ S`-representation induced on the invariants of `Ï|_S`. -/
@[simps]
noncomputable def infFunctor : Rep k G â¥¤ Rep k (G â§¸ S) where
  obj A := inf A S
  map f := infMap S f

end Inf

section Coinvariants

variable {k G} {A B C : Rep k G} {n : â„•} {V : Type u} [AddCommGroup V] [Module k V]

open Representation

/-- A `G`-representation morphism `A âŸ¶ trivial(V)` induces a linear map `A_G â†’â‚—[k] V`. -/
def coinvariantsLift (f : A âŸ¶ Rep.trivial k G V) :
    coinvariants A.Ï â†’â‚—[k] V :=
  Representation.coinvariantsLift _ (hom f) f.comm

/-- A `G`-representation morphism `A âŸ¶ B` induces a linear map `A_G â†’â‚—[k] B_G`. -/
def coinvariantsMap (f : A âŸ¶ B) :
    coinvariants A.Ï â†’â‚—[k] coinvariants B.Ï :=
  Representation.coinvariantsLift _ (Submodule.mkQ _ âˆ˜â‚— hom f) fun g => LinearMap.ext fun x => by
    simpa [hom_comm_apply'', Submodule.Quotient.eq]
    using mem_coinvariantsKer_of_eq B.Ï g (hom f x) _ rfl

@[simp]
theorem coinvariantsMap_mkQ (f : A âŸ¶ B) :
    coinvariantsMap f âˆ˜â‚— (coinvariantsKer A.Ï).mkQ = (coinvariantsKer B.Ï).mkQ âˆ˜â‚— hom f := rfl

@[simp]
theorem coinvariantsMap_apply (f : A âŸ¶ B) (x : A) :
    coinvariantsMap f (Submodule.Quotient.mk x) = Submodule.Quotient.mk (hom f x) := rfl

@[simp]
theorem coinvariantsMap_id (A : Rep k G) :
    coinvariantsMap (ğŸ™ A) = LinearMap.id := by
  ext; simp

@[simp]
theorem coinvariantsMap_comp (f : A âŸ¶ B) (g : B âŸ¶ C) :
    coinvariantsMap (f â‰« g) = coinvariantsMap g âˆ˜â‚— coinvariantsMap f := by
  ext; simp

variable (A B)

/-- For a representation `A` of a finite group `G`, the norm map `A âŸ¶ A` induces a linear map
`A_G â†’â‚—[k] Aá´³`. -/
noncomputable def liftRestrictNorm [Fintype G] :
    A.Ï.coinvariants â†’â‚—[k] A.Ï.invariants :=
  A.Ï.coinvariantsLift ((hom <| norm A).codRestrict _
    fun a g => congr($(Ï_comp_norm A g) a)) fun g => by ext x; exact congr($(norm_comp_Ï A g) x)

variable (k G)

/-- The functor sending a representation to its coinvariants. -/
@[simps]
def coinvariantsFunctor : Rep k G â¥¤ ModuleCat k where
  obj A := ModuleCat.of k (A.Ï.coinvariants)
  map f := coinvariantsMap f

/-- The adjunction between the functor sending a representation to its coinvariants and the functor
equipping a module with the trivial representation. -/
noncomputable def coinvariantsAdjunction : coinvariantsFunctor k G âŠ£ trivialFunctor :=
  Adjunction.mkOfHomEquiv {
    homEquiv := fun X Y => {
      toFun := fun f => {
        hom := f âˆ˜â‚— X.Ï.coinvariantsKer.mkQ
        comm := fun g => by
          ext x
          exact congr(f $((Submodule.Quotient.eq <| X.Ï.coinvariantsKer).2
            (X.Ï.mem_coinvariantsKer_of_eq g x _ rfl))) }
      invFun := fun f => coinvariantsLift f
      left_inv := fun x => Submodule.linearMap_qext _ rfl
      right_inv := fun x => Action.Hom.ext rfl }
    homEquiv_naturality_left_symm := by intros; apply Submodule.linearMap_qext; rfl
    homEquiv_naturality_right := by intros; rfl }

instance : (coinvariantsFunctor k G).PreservesZeroMorphisms where
  map_zero _ _ := Submodule.linearMap_qext _ rfl

noncomputable instance : Limits.PreservesColimits (coinvariantsFunctor k G) :=
  (coinvariantsAdjunction k G).leftAdjointPreservesColimits

end Coinvariants

/-- Given a finite group `G`, this is the natural transformation sending a `G`-representation `A`
to the map `A_G â†’â‚—[k] Aá´³` induced by the norm map on `A`. -/
@[simps]
noncomputable def liftRestrictNormNatTrans [Fintype G] :
    coinvariantsFunctor k G âŸ¶ invariantsFunctor k G where
  app A := liftRestrictNorm A
  naturality _ _ _ := Submodule.linearMap_qext _ <| LinearMap.ext fun x => Subtype.ext <| by
    simp [norm, ModuleCat.comp_def, ModuleCat.hom_def, ModuleCat.coe_of, liftRestrictNorm,
      hom_comm_apply'']

section Coinf

variable {k G} (S : Subgroup G) [S.Normal]

/-- Given a normal subgroup `S â‰¤ G`, a `G`-representation `Ï` induces a `G â§¸ S`-representation on
the coinvariants of `Ï|_S`. -/
abbrev coinf := Rep.of (A.Ï.coinf S)

/-- Given a normal subgroup `S â‰¤ G`, a `G`-representation morphism `f : A âŸ¶ B` induces a
`G â§¸ S`-representation morphism `coinvariants Ï_A|_S âŸ¶ coinvariants Ï_B|_S`. -/
noncomputable abbrev coinfMap {A B : Rep k G} (Ï† : A âŸ¶ B) :
    coinf A S âŸ¶ coinf B S :=
  mkHom ((coinvariantsFunctor k S).map ((Action.res _ S.subtype).map Ï†))
    fun g => QuotientGroup.induction_on g fun g =>
    Submodule.linearMap_qext _ <| by
      ext x
      refine (Submodule.Quotient.eq _).2 ?_
      simp [coe_res_obj, â† hom_comm_apply'']

/-- Given a normal subgroup `S â‰¤ G`, this functor sends a `G`-representation `Ï` to the
`G â§¸ S`-representation induced on the coinvariants of `Ï|_S`. -/
@[simps]
noncomputable def coinfFunctor : Rep k G â¥¤ Rep k (G â§¸ S) where
  obj A := coinf A S
  map f := coinfMap S f
  map_id _ := Rep.ext <| coinvariantsMap_id _
  map_comp _ _ := Rep.ext <| by simp

end Coinf
end Rep
