/-
Copyright (c) 2023 Amelia Livingston. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Amelia Livingston
-/
import Mathlib.Algebra.Homology.Opposite
import Mathlib.Algebra.Homology.ShortComplex.HomologicalComplex
import Mathlib.RepresentationTheory.GroupCohomology.Resolution
import Mathlib.Tactic.CategoryTheory.Slice
import Mathlib.CategoryTheory.Abelian.LeftDerived

noncomputable section

universe u
section
variable (R A B Î± : Type*) [CommRing R] [AddCommGroup A] [AddCommGroup B]
  [Module R A] [Module R B]

theorem Submodule.Quotient.mk_sum {Î¹ : Type*} (S : Submodule R A)
    (s : Finset Î¹) (f : Î¹ â†’ A) :
    Submodule.Quotient.mk (p := S) (s.sum f) = s.sum (fun i => Submodule.Quotient.mk (f i)) :=
  map_sum (Submodule.mkQ S) _ _

open CategoryTheory CategoryTheory.Limits MonoidalCategory

namespace Rep

variable {k G : Type u} [CommRing k] [Group G] (A B C : Rep k G) {n : â„•} (Î± : Type u)

def finsuppTensorLeft [DecidableEq Î±] :
    A.finsupp Î± âŠ— B â‰… (A âŠ— B).finsupp Î± :=
  mkIso' (TensorProduct.finsuppLeft k A B Î±) fun g =>
    TensorProduct.ext <| Finsupp.lhom_ext fun a b => by
    ext (x : B)
    simp only [Equivalence.symm_inverse, Action.functorCategoryEquivalence_functor,
      Action.FunctorCategoryEquivalence.functor_obj_obj, coe_def, tensor_Ï]
    simp [coe_tensor, tensor_Ï', TensorProduct.finsuppLeft_apply_tmul]

variable {A B}

@[simp]
theorem finsuppTensorLeft_hom_apply_tmul [DecidableEq Î±] (x : Î± â†’â‚€ A) (y : B) :
    hom (finsuppTensorLeft A B Î±).hom (x âŠ—â‚œ y) = x.sum fun i a => Finsupp.single i (a âŠ—â‚œ y) :=
  TensorProduct.finsuppLeft_apply_tmul _ _

@[simp]
theorem finsuppTensorLeft_inv_apply_single [DecidableEq Î±] (a : Î±) (x : A) (y : B) :
    hom (finsuppTensorLeft A B Î±).inv (Finsupp.single a (x âŠ—â‚œ y)) = Finsupp.single a x âŠ—â‚œ y :=
  TensorProduct.finsuppLeft_symm_apply_single _ _ _

variable (A B)

def finsuppTensorRight [DecidableEq Î±] :
    A âŠ— B.finsupp Î± â‰… (A âŠ— B).finsupp Î± :=
  mkIso' (TensorProduct.finsuppRight k A B Î±) fun g =>
    TensorProduct.ext <| LinearMap.ext fun x => Finsupp.lhom_ext fun a b => by
    simp only [Equivalence.symm_inverse, Action.functorCategoryEquivalence_functor,
      Action.FunctorCategoryEquivalence.functor_obj_obj, coe_def, tensor_Ï]
    simp [coe_tensor, tensor_Ï', TensorProduct.finsuppRight_apply_tmul]

variable {A B}

@[simp]
theorem finsuppTensorRight_hom_apply_tmul [DecidableEq Î±] (x : A) (y : Î± â†’â‚€ B) :
    hom (finsuppTensorRight A B Î±).hom (x âŠ—â‚œ y) = y.sum fun i b => Finsupp.single i (x âŠ—â‚œ b) :=
  TensorProduct.finsuppRight_apply_tmul _ _

@[simp]
theorem finsuppTensorRight_inv_apply_single [DecidableEq Î±] (a : Î±) (x : A) (y : B) :
    hom (finsuppTensorRight A B Î±).inv (Finsupp.single a (x âŠ—â‚œ y)) = x âŠ—â‚œ Finsupp.single a y :=
  TensorProduct.finsuppRight_symm_apply_single _ _ _

end Rep
namespace Representation

variable {k G V W : Type*} [CommRing k] [Group G]
    [AddCommGroup V] [Module k V] (Ï : Representation k G V)
    [AddCommGroup W] [Module k W] (Ï„ : Representation k G W)

def inv : Representation k Gáµáµ’áµ– V :=
  Ï.comp (MulEquiv.inv' G).symm.toMonoidHom

@[simp] lemma inv_apply (g : Gáµáµ’áµ–) (x : V) :
  Ï.inv g x = Ï g.unopâ»Â¹ x := rfl

abbrev coinvariantsKer := Submodule.span k (Set.range <| fun (x : G Ã— V) => Ï x.1 x.2 - x.2)

lemma mem_coinvariantsKer (g : G) (x a : V) (h : Ï g x - x = a) : a âˆˆ coinvariantsKer Ï :=
Submodule.subset_span âŸ¨(g, x), hâŸ©

abbrev coinvariants := V â§¸ coinvariantsKer Ï

def coinvariantsLift (f : V â†’â‚—[k] W) (h : âˆ€ (x : G), f âˆ˜â‚— Ï x = f) :
    Ï.coinvariants â†’â‚—[k] W :=
  Submodule.liftQ _ f <| Submodule.span_le.2 fun x âŸ¨âŸ¨g, yâŸ©, hyâŸ© => by
    simpa only [â† hy, SetLike.mem_coe, LinearMap.mem_ker, map_sub, sub_eq_zero, LinearMap.coe_comp,
      Function.comp_apply] using LinearMap.ext_iff.1 (h g) y

@[simp] theorem coinvariantsLift_mkQ (f : V â†’â‚—[k] W) {h : âˆ€ (x : G), f âˆ˜â‚— Ï x = f} :
  coinvariantsLift Ï f h âˆ˜â‚— (coinvariantsKer Ï).mkQ = f := rfl

@[simp]
theorem coinvariantsLift_apply (f : V â†’â‚—[k] W) {h : âˆ€ (x : G), f âˆ˜â‚— Ï x = f} (x : V) :
  coinvariantsLift Ï f h (Submodule.Quotient.mk x) = f x := rfl
end Representation

namespace Rep

variable {k G : Type u} [CommRing k] [Group G] {A B C D : Rep k G} {n : â„•} (Î± : Type u)
  {V : Type u} [AddCommGroup V] [Module k V]
open Representation
def coinvariantsLift (f : A âŸ¶ (Rep.trivial k G V)) :
    coinvariants A.Ï â†’â‚—[k] V :=
  Representation.coinvariantsLift _ (hom f) f.comm

def coinvariantsMap (f : A âŸ¶ B) :
    coinvariants A.Ï â†’â‚—[k] coinvariants B.Ï :=
  Representation.coinvariantsLift _ (Submodule.mkQ _ âˆ˜â‚— hom f) fun g => LinearMap.ext fun x => by
    simpa [hom_comm_apply'', Submodule.Quotient.eq]
    using mem_coinvariantsKer B.Ï g (hom f x) _ rfl

@[simp] theorem coinvariantsMap_mkQ (f : A âŸ¶ B) :
  coinvariantsMap f âˆ˜â‚— (coinvariantsKer A.Ï).mkQ = (coinvariantsKer B.Ï).mkQ âˆ˜â‚— hom f := rfl

@[simp]
theorem coinvariantsMap_apply (f : A âŸ¶ B) (x : A) :
  coinvariantsMap f (Submodule.Quotient.mk x) = Submodule.Quotient.mk (hom f x) := rfl

variable (Î± : Type u)
variable (A B)

@[simps]
def coinvariantsHomEquiv :
    (coinvariants A.Ï â†’â‚—[k] B) â‰ƒ (A âŸ¶ trivial k G B) where
  toFun := fun f => {
    hom := f âˆ˜â‚— (coinvariantsKer A.Ï).mkQ
    comm := fun g => by
      ext x
      exact congr(f $((Submodule.Quotient.eq <| coinvariantsKer A.Ï).2
        (mem_coinvariantsKer _ g x _ rfl))) }
  invFun := fun f => coinvariantsLift f
  left_inv := fun x => Submodule.linearMap_qext _ rfl
  right_inv := fun x => Action.Hom.ext rfl

@[simp] def coinvariantsToFinsupp :
    coinvariants (A.finsupp Î±).Ï â†’â‚—[k] Î± â†’â‚€ coinvariants A.Ï :=
(Representation.coinvariantsLift _ (Finsupp.mapRange.linearMap (Submodule.mkQ _)) <| fun g =>
  Finsupp.lhom_ext fun i x => by
  simp [Finsupp.mapRange.linearMap, â† (Submodule.Quotient.eq _).2
    (mem_coinvariantsKer A.Ï g x _ rfl), finsupp])

@[simp] def finsuppToCoinvariants :
    (Î± â†’â‚€ coinvariants A.Ï) â†’â‚—[k] coinvariants (A.finsupp Î±).Ï :=
  Finsupp.lsum (R := k) k fun a => coinvariantsMap (lsingle A a)

@[simps]
def coinvariantsFinsuppLEquiv :
    coinvariants (A.finsupp Î±).Ï â‰ƒâ‚—[k] Î± â†’â‚€ coinvariants A.Ï where
  toFun := coinvariantsToFinsupp A Î±
  map_add' := map_add _
  map_smul' := map_smul _
  invFun := finsuppToCoinvariants A Î±
  left_inv := fun x => by
    show (finsuppToCoinvariants A Î± âˆ˜â‚— _) x = LinearMap.id (R := k) x
    refine LinearMap.ext_iff.1 (Submodule.linearMap_qext _ <| Finsupp.lhom_ext fun a x => ?_) x
    have := coinvariantsMap_apply (A.lsingle a) x
    simp_all
  right_inv := fun x => by
    show (coinvariantsToFinsupp A Î± âˆ˜â‚— _) x = LinearMap.id (R := k) x
    refine LinearMap.ext_iff.1 (Finsupp.lhom_ext fun a x => Quotient.inductionOn'
      (x : coinvariants A.Ï) fun y => ?_) x
    simp [coinvariantsMap, Submodule.Quotient.mk''_eq_mk]

variable {A B}

lemma coinvariants_whisker_comm (f : A âŸ¶ B) (g : C âŸ¶ D) :
    coinvariantsMap (B â— g) âˆ˜â‚— coinvariantsMap (f â–· C)
      = coinvariantsMap (f â–· D)
        âˆ˜â‚— coinvariantsMap (A â— g) :=
  Submodule.linearMap_qext _ <| TensorProduct.ext' fun _ _ => by rfl

variable (A)

def coinvariantsTensorHom : coinvariants (A âŠ— leftRegular k G).Ï â†’â‚—[k] A :=
  Representation.coinvariantsLift _ (TensorProduct.lift (Finsupp.linearCombination _
    (fun g => A.Ï gâ»Â¹)) âˆ˜â‚— (TensorProduct.comm _ _ _).toLinearMap) fun g => TensorProduct.ext <|
      LinearMap.ext fun (x : A) => Finsupp.lhom_ext fun a y => by
    simp only [Equivalence.symm_inverse, Action.functorCategoryEquivalence_functor,
      Action.FunctorCategoryEquivalence.functor_obj_obj, coe_def, tensor_Ï]
    simp [coe_tensor, tensor_Ï', TensorProduct.finsuppLeft_apply_tmul]

variable {A}

@[simp] lemma coinvariantsTensorHom_apply (x : A) (g : G) (r : k) :
    coinvariantsTensorHom A (Submodule.Quotient.mk (p := coinvariantsKer _)
      (x âŠ—â‚œ Finsupp.single g r)) = r â€¢ A.Ï gâ»Â¹ x :=
  congr($(Finsupp.linearCombination_single k (v := fun g => A.Ï gâ»Â¹) r g) x)

variable (A)

def toCoinvariantsTensor : A â†’â‚—[k] coinvariants (A âŠ— leftRegular k G).Ï :=
  Submodule.mkQ _ âˆ˜â‚— (TensorProduct.mk k A (G â†’â‚€ k)).flip (Finsupp.single 1 1)

variable {A}

@[simp] lemma toCoinvariantsTensor_apply (x : A) :
    toCoinvariantsTensor A x = Submodule.Quotient.mk (x âŠ—â‚œ Finsupp.single (1 : G) (1 : k)) := rfl

variable (A)

@[simps]
def coinvariantsTensorLEquiv : (coinvariants (A âŠ— leftRegular k G).Ï) â‰ƒâ‚—[k] A where
  toFun := coinvariantsTensorHom A
  map_add' := map_add _
  map_smul' := map_smul _
  invFun := toCoinvariantsTensor A
  left_inv := LinearMap.congr_fun (f := (toCoinvariantsTensor A) âˆ˜â‚— coinvariantsTensorHom A)
    (g := LinearMap.id) <|
    Submodule.linearMap_qext _ <| TensorProduct.ext <|
      LinearMap.ext fun (a : A) => Finsupp.lhom_ext fun g r => (Submodule.Quotient.eq _).2 <| by
      apply mem_coinvariantsKer (A.Ï.tprod (Representation.leftRegular k G)) gâ»Â¹
        (a âŠ—â‚œ[k] Finsupp.single g r)
      have := coinvariantsTensorHom_apply a g r
      simp_all [coe_tensor, TensorProduct.smul_tmul', TensorProduct.smul_tmul]
  right_inv := fun x => by simp [coe_def, coe_tensor, toCoinvariantsTensor,
    coinvariantsTensorHom]

variable (Î± : Type u) [DecidableEq Î±]

open TensorProduct

def coinvariantsTensorFreeToFinsupp :
    coinvariants (A âŠ— (Rep.free k G Î±)).Ï â†’â‚—[k] (Î± â†’â‚€ A) :=
  (coinvariantsFinsuppLEquiv _ Î± â‰ªâ‰«â‚— Finsupp.lcongr (Equiv.refl Î±)
    (coinvariantsTensorLEquiv A)).toLinearMap âˆ˜â‚— coinvariantsMap (finsuppTensorRight A
      (leftRegular k G) Î±).hom

variable {A Î±}

@[simp] lemma coinvariantsTensorFreeToFinsupp_apply (x : A) (i : Î±) (g : G) (r : k) :
    coinvariantsTensorFreeToFinsupp A Î± (Submodule.Quotient.mk
      (x âŠ—â‚œ Finsupp.single i (Finsupp.single g r)))
      = Finsupp.single i (r â€¢ A.Ï gâ»Â¹ x) := by
  have h := finsuppTensorRight_hom_apply_tmul (B := leftRegular k G)
    Î± x (Finsupp.single i (Finsupp.single g r))
  have h' := coinvariantsTensorHom_apply x g r
  simp_all [coinvariantsTensorFreeToFinsupp, coinvariantsMap,
    coinvariantsFinsuppLEquiv, Finsupp.mapRange.linearMap, coinvariantsTensorLEquiv]

variable (A Î±)

def finsuppToCoinvariantsTensorFree :
    (Î± â†’â‚€ A) â†’â‚—[k] coinvariants (A âŠ— (Rep.free k G Î±)).Ï :=
  coinvariantsMap ((finsuppTensorRight A (leftRegular k G) Î±)).inv âˆ˜â‚—
    (coinvariantsFinsuppLEquiv _ Î± â‰ªâ‰«â‚— Finsupp.lcongr (Equiv.refl Î±)
      (coinvariantsTensorLEquiv A)).symm.toLinearMap

variable {A Î±}

@[simp] lemma finsuppToCoinvariantsTensorFree_apply (i : Î±) (x : A) :
    finsuppToCoinvariantsTensorFree A Î± (Finsupp.single i x)
      = Submodule.Quotient.mk (x âŠ—â‚œ Finsupp.single i (Finsupp.single (1 : G) (1 : k))) := by
  simpa [finsuppToCoinvariantsTensorFree, coinvariantsMap, coinvariantsFinsuppLEquiv]
    using congr(Submodule.Quotient.mk $(finsuppTensorRight_inv_apply_single (A := A)
      (B := leftRegular k G) Î± i x (Finsupp.single 1 1)))

variable (A Î±)

open Finsupp
@[simps] def coinvariantsTensorFreeLEquiv :
    coinvariants (A âŠ— Rep.free k G Î±).Ï â‰ƒâ‚—[k] (Î± â†’â‚€ A) where
      toFun := coinvariantsTensorFreeToFinsupp A Î±
      map_add' := map_add _
      map_smul' := map_smul _
      invFun := finsuppToCoinvariantsTensorFree A Î±
      left_inv := fun x => by
        show (finsuppToCoinvariantsTensorFree A Î± âˆ˜â‚— _) x = LinearMap.id (R := k) x
        refine LinearMap.ext_iff.1 (Submodule.linearMap_qext _ <| TensorProduct.ext <|
          LinearMap.ext fun (a : A) => lhom_ext' fun i => lhom_ext fun g r => ?_) x
        simp only [LinearMap.coe_comp,
          Function.comp_apply, lsingle_apply, LinearMap.comprâ‚‚_apply, mk_apply, LinearMap.coe_mk,
          AddHom.coe_mk, Submodule.mkQ_apply, coinvariantsTensorFreeToFinsupp_apply a i g r,
          finsuppToCoinvariantsTensorFree_apply, LinearMap.id_comp, Submodule.Quotient.eq]
        refine mem_coinvariantsKer (A âŠ— Rep.free k G Î±).Ï gâ»Â¹ (a âŠ—â‚œ[k] single i (single g r)) _
          (sub_left_inj.2 ?_)
        rw [tensor_Ï]
        simp [coe_tensor, TensorProduct.smul_tmul]
      right_inv := fun x => by
        show (coinvariantsTensorFreeToFinsupp A Î± âˆ˜â‚— _) x = LinearMap.id (R := k) x
        refine LinearMap.ext_iff.1 (Finsupp.lhom_ext fun i a => ?_) x
        simp [coinvariantsTensorFreeToFinsupp_apply a i 1 1]

def d (n : â„•) : ((Fin (n + 1) â†’ G) â†’â‚€ A) â†’â‚—[k] (Fin n â†’ G) â†’â‚€ A :=
  Finsupp.lsum (R := k) k fun g => Finsupp.lsingle (fun i => g i.succ) âˆ˜â‚— A.Ï (g 0)â»Â¹
    + Finset.univ.sum fun j : Fin (n + 1) =>
      (-1 : k) ^ ((j : â„•) + 1) â€¢ Finsupp.lsingle (Fin.contractNth j (Â· * Â·) g)

theorem d_apply (n : â„•) (x : (Fin (n + 1) â†’ G) â†’â‚€ A) :
    A.d n x = x.sum fun g a => Finsupp.single (fun i => g i.succ) (A.Ï (g 0)â»Â¹ a)
      + Finset.univ.sum fun j : Fin (n + 1) =>
        (-1 : k) ^ ((j : â„•) + 1) â€¢ Finsupp.single (Fin.contractNth j (Â· * Â·) g) a := by
  ext
  simp [d]

variable (k G)

@[simps] def coinvariants : Rep k G â¥¤ ModuleCat k where
  obj := fun A => ModuleCat.of k (Representation.coinvariants A.Ï)
  map := fun f => coinvariantsMap f
  map_id := fun X => by
    ext x
    refine Quotient.inductionOn' x (fun y => rfl)
  map_comp := fun f g => by
    ext x
    refine Quotient.inductionOn' x (fun y => rfl)

instance : (coinvariants k G).Additive where
  map_add := fun {_ _ _ _} => LinearMap.ext fun x => Quotient.inductionOn' x (fun _ => rfl)

variable {k G}

abbrev coinvariantsFinsuppIso (A : Rep k G) (Î± : Type u) :
    (coinvariants k G).obj (A.finsupp Î±) â‰… ModuleCat.of k (Î± â†’â‚€ (coinvariants k G).obj A) :=
  (coinvariantsFinsuppLEquiv A Î±).toModuleIso

abbrev coinvariantsTensorLeftRegular (A : Rep k G) :
    (coinvariants k G).obj (A âŠ— Rep.leftRegular k G) â‰… A.V :=
  A.coinvariantsTensorLEquiv.toModuleIso

open MonoidalCategory

abbrev coinvariantsTensorFreeIso (A : Rep k G) (Î± : Type u) [DecidableEq Î±] :
    (coinvariants k G).obj (A âŠ— Rep.free k G Î±)
      â‰… ModuleCat.of k (Î± â†’â‚€ A) :=
  (A.coinvariantsTensorFreeLEquiv Î±).toModuleIso

variable (k G)

@[simps] def tensor : Rep k G â¥¤ Rep k G â¥¤ ModuleCat k :=
{ obj := fun A => MonoidalCategory.tensorLeft A â‹™ coinvariants k G
  map := fun f => {
    app := fun A => coinvariantsMap (f âŠ— ğŸ™ A)
    naturality := fun A B g => (coinvariants_whisker_comm f g).symm }
  map_id := fun A => NatTrans.ext <| by
    ext B : 1
    dsimp only
    rw [MonoidalCategory.tensor_id]
    exact (coinvariants k G).map_id _
  map_comp := fun f g => NatTrans.ext <| by
    ext B : 1
    dsimp only
    rw [MonoidalCategory.comp_tensor_id]
    exact (coinvariants k G).map_comp _ _ }

instance (A : Rep k G) : ((tensor k G).obj A).Additive := by
  unfold tensor
  infer_instance

def Tor (n : â„•) : Rep k G â¥¤ Rep k G â¥¤ ModuleCat k where
  obj X := Functor.leftDerived ((tensor k G).obj X) n
  map f := NatTrans.leftDerived ((tensor k G).map f) n

variable {k G}
variable (A : Rep k G)

def tensorChainComplex (Î± : Type*) [AddRightCancelSemigroup Î±] [One Î±] :
  ChainComplex (Rep k G) Î± â¥¤ ChainComplex (ModuleCat k) Î± :=
Functor.mapHomologicalComplex ((tensor k G).obj A) _

def torIso (B : Rep k G) (P : ProjectiveResolution B) (n : â„•) :
    ((Tor k G n).obj A).obj B â‰… ((tensorChainComplex A â„•).obj P.complex).homology n :=
  ProjectiveResolution.isoLeftDerivedObj P ((tensor k G).obj A) n

def tensorBarResolution := (tensorChainComplex A â„•).obj (groupHomology.barResolution k G)

def tensorStdResolution := (tensorChainComplex A â„•).obj (groupCohomology.resolution k G)

open groupHomology
theorem d_eq [DecidableEq G] :
    A.d n = (coinvariantsTensorFreeIso A (Fin (n + 1) â†’ G)).inv â‰«
      (tensorBarResolution A).d (n + 1) n â‰« (coinvariantsTensorFreeIso A (Fin n â†’ G)).hom := by
  ext g a : 2
  show _ = A.coinvariantsTensorFreeToFinsupp (Fin n â†’ G) ((tensorBarResolution A).d _ _
    (A.finsuppToCoinvariantsTensorFree _ _))
  simp only [Finsupp.lsingle_apply, finsuppToCoinvariantsTensorFree_apply, tensorBarResolution,
    tensorChainComplex, Functor.mapHomologicalComplex_obj_X, ChainComplex.of_x,
    Functor.mapHomologicalComplex_obj_d, barResolution.d_def]
  show _ = A.coinvariantsTensorFreeToFinsupp (Fin n â†’ G)
    (Submodule.Quotient.mk (a âŠ—â‚œ[k] hom (groupHomology.d k G n) (single _ _)))
  have := d_single (k := k) g
  simp_all [TensorProduct.tmul_add, TensorProduct.tmul_sum, Submodule.Quotient.mk_sum, d,
    coinvariantsTensorFreeToFinsupp_apply (Î± := Fin n â†’ G) a]

noncomputable abbrev inhomogeneousChains [DecidableEq G] :
    ChainComplex (ModuleCat k) â„• :=
  ChainComplex.of (fun n => ModuleCat.of k ((Fin n â†’ G) â†’â‚€ A))
    (fun n => A.d n) fun n => by
    simp only [d_eq]
    slice_lhs 3 4 => { rw [Iso.hom_inv_id] }
    slice_lhs 2 4 => { rw [Category.id_comp, (tensorBarResolution A).d_comp_d] }
    simp

@[simp]
theorem inhomogeneousChains.d_def [DecidableEq G] (n : â„•) :
    (inhomogeneousChains A).d (n + 1) n = A.d n :=
  ChainComplex.of_d _ _ _ _

def inhomogeneousChainsIsotensorBar [DecidableEq G] :
    inhomogeneousChains A â‰… tensorBarResolution A := by
  refine HomologicalComplex.Hom.isoOfComponents ?_ ?_
  Â· intro i
    apply (coinvariantsTensorFreeIso A (Fin i â†’ G)).symm
  rintro i j (h : j + 1 = i)
  subst h
  simp only [Iso.symm_hom, inhomogeneousChains.d_def, d_eq, Category.assoc]
  slice_rhs 2 4 => { rw [Iso.hom_inv_id, Category.comp_id] }

variable [DecidableEq G]

def inhomogeneousChainsIsotensorStd  : inhomogeneousChains A â‰… tensorStdResolution A :=
  inhomogeneousChainsIsotensorBar A â‰ªâ‰« (tensorChainComplex A â„•).mapIso (barResolutionIso k G)

abbrev cycles (n : â„•) : ModuleCat k := (inhomogeneousChains A).cycles n

abbrev iCycles (n : â„•) : cycles A n âŸ¶ ModuleCat.of k ((Fin n â†’ G) â†’â‚€ A) :=
  (inhomogeneousChains A).iCycles n

abbrev toCycles (i j : â„•) : ModuleCat.of k ((Fin i â†’ G) â†’â‚€ A) âŸ¶ cycles A j :=
  (inhomogeneousChains A).toCycles i j

def groupHomology (n : â„•) : ModuleCat k :=
  (inhomogeneousChains A).homology n

abbrev groupHomologyÏ€ (n : â„•) :
    cycles A n âŸ¶ groupHomology A n :=
  (inhomogeneousChains A).homologyÏ€ n

def groupHomologyIsoTor [Group G] (A : Rep k G) (n : â„•) :
    groupHomology A n â‰… ((Tor k G n).obj A).obj (Rep.trivial k G k) :=
  isoOfQuasiIsoAt (HomotopyEquiv.ofIso (inhomogeneousChainsIsotensorStd A)).hom n â‰ªâ‰«
    (torIso A (Rep.trivial k G k) (groupCohomology.projectiveResolution k G) n).symm
