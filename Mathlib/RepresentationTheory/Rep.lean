/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.Algebra.Category.ModuleCat.Adjunctions
import Mathlib.Algebra.Category.ModuleCat.Limits
import Mathlib.Algebra.Category.ModuleCat.Colimits
import Mathlib.Algebra.Category.ModuleCat.Monoidal.Symmetric
import Mathlib.CategoryTheory.Elementwise
import Mathlib.RepresentationTheory.Action.Monoidal
import Mathlib.RepresentationTheory.Basic

#align_import representation_theory.Rep from "leanprover-community/mathlib"@"cec81510e48e579bde6acd8568c06a87af045b63"

/-!
# `Rep k G` is the category of `k`-linear representations of `G`.

If `V : Rep k G`, there is a coercion that allows you to treat `V` as a type,
and this type comes equipped with a `Module k V` instance.
Also `V.œÅ` gives the homomorphism `G ‚Üí* (V ‚Üí‚Çó[k] V)`.

Conversely, given a homomorphism `œÅ : G ‚Üí* (V ‚Üí‚Çó[k] V)`,
you can construct the bundled representation as `Rep.of œÅ`.

We construct the categorical equivalence `Rep k G ‚âå ModuleCat (MonoidAlgebra k G)`.
We verify that `Rep k G` is a `k`-linear abelian symmetric monoidal category with all (co)limits.
-/

suppress_compilation

universe u

open CategoryTheory

open CategoryTheory.Limits

/-- The category of `k`-linear representations of a monoid `G`. -/
abbrev Rep (k G : Type u) [Ring k] [Monoid G] :=
  Action (ModuleCat.{u} k) (MonCat.of G)
set_option linter.uppercaseLean3 false in
#align Rep Rep

instance (k G : Type u) [CommRing k] [Monoid G] : Linear k (Rep k G) := by infer_instance

namespace Rep

variable {k G : Type u} [CommRing k]

section

variable [Monoid G]

instance : CoeSort (Rep k G) (Type u) :=
  ConcreteCategory.hasCoeToSort _

instance (V : Rep k G) : AddCommGroup V := by
  change AddCommGroup ((forget‚ÇÇ (Rep k G) (ModuleCat k)).obj V); infer_instance

instance (V : Rep k G) : Module k V := by
  change Module k ((forget‚ÇÇ (Rep k G) (ModuleCat k)).obj V)
  infer_instance

/-- Specialize the existing `Action.œÅ`, changing the type to `Representation k G V`.
-/
def œÅ (V : Rep k G) : Representation k G V :=
-- porting note: was `V.œÅ`
  Action.œÅ V
set_option linter.uppercaseLean3 false in
#align Rep.œÅ Rep.œÅ

/-- Lift an unbundled representation to `Rep`. -/
def of {V : Type u} [AddCommGroup V] [Module k V] (œÅ : G ‚Üí* V ‚Üí‚Çó[k] V) : Rep k G :=
  ‚ü®ModuleCat.of k V, œÅ‚ü©
set_option linter.uppercaseLean3 false in
#align Rep.of Rep.of

@[simp]
theorem coe_of {V : Type u} [AddCommGroup V] [Module k V] (œÅ : G ‚Üí* V ‚Üí‚Çó[k] V) :
    (of œÅ : Type u) = V :=
  rfl
set_option linter.uppercaseLean3 false in
#align Rep.coe_of Rep.coe_of

@[simp]
theorem of_œÅ {V : Type u} [AddCommGroup V] [Module k V] (œÅ : G ‚Üí* V ‚Üí‚Çó[k] V) : (of œÅ).œÅ = œÅ :=
  rfl
set_option linter.uppercaseLean3 false in
#align Rep.of_œÅ Rep.of_œÅ

theorem Action_œÅ_eq_œÅ {A : Rep k G} : Action.œÅ A = A.œÅ :=
  rfl
set_option linter.uppercaseLean3 false in
#align Rep.Action_œÅ_eq_œÅ Rep.Action_œÅ_eq_œÅ

/-- Allows us to apply lemmas about the underlying `œÅ`, which would take an element `g : G` rather
than `g : MonCat.of G` as an argument. -/
theorem of_œÅ_apply {V : Type u} [AddCommGroup V] [Module k V] (œÅ : Representation k G V)
    (g : MonCat.of G) : (Rep.of œÅ).œÅ g = œÅ (g : G) :=
  rfl
set_option linter.uppercaseLean3 false in
#align Rep.of_œÅ_apply Rep.of_œÅ_apply

@[simp]
theorem œÅ_inv_self_apply {G : Type u} [Group G] (A : Rep k G) (g : G) (x : A) :
    A.œÅ g‚Åª¬π (A.œÅ g x) = x :=
  show (A.œÅ g‚Åª¬π * A.œÅ g) x = x by rw [‚Üê map_mul, inv_mul_self, map_one, LinearMap.one_apply]
set_option linter.uppercaseLean3 false in
#align Rep.œÅ_inv_self_apply Rep.œÅ_inv_self_apply

@[simp]
theorem œÅ_self_inv_apply {G : Type u} [Group G] {A : Rep k G} (g : G) (x : A) :
    A.œÅ g (A.œÅ g‚Åª¬π x) = x :=
  show (A.œÅ g * A.œÅ g‚Åª¬π) x = x by rw [‚Üê map_mul, mul_inv_self, map_one, LinearMap.one_apply]
set_option linter.uppercaseLean3 false in
#align Rep.œÅ_self_inv_apply Rep.œÅ_self_inv_apply

theorem hom_comm_apply {A B : Rep k G} (f : A ‚ü∂ B) (g : G) (x : A) :
    f.hom (A.œÅ g x) = B.œÅ g (f.hom x) :=
  LinearMap.ext_iff.1 (f.comm g) x
set_option linter.uppercaseLean3 false in
#align Rep.hom_comm_apply Rep.hom_comm_apply

/-- Alternative constructor for representation morphisms with less categorical terms. -/
def mkHom {A B : Rep k G} (f : A ‚Üí‚Çó[k] B)
    (hf : ‚àÄ g, f ‚àò‚Çó A.œÅ g = B.œÅ g ‚àò‚Çó f) : A ‚ü∂ B where
  hom := ModuleCat.ofHom f
  comm := hf

@[simp] lemma mkHom_hom_apply {A B : Rep k G} (f : A ‚Üí‚Çó[k] B)
    {hf : ‚àÄ g, f ‚àò‚Çó A.œÅ g = B.œÅ g ‚àò‚Çó f} (x : A) :
    (mkHom f hf).hom x = f x := rfl

/-- Alternative constructor for representation isomorphisms with less categorical terms. -/
def mkIso {A B : Rep k G} (f : A ‚âÉ‚Çó[k] B)
    (hf : ‚àÄ g, f ‚àò‚Çó A.œÅ g = B.œÅ g ‚àò‚Çó f) : A ‚âÖ B :=
Action.mkIso f.toModuleIso hf

lemma mkHom_ext_left {A B : Rep k G} (f : A ‚Üí‚Çó[k] B) (g : A ‚ü∂ B)
    {hf : ‚àÄ g, f ‚àò‚Çó A.œÅ g = B.œÅ g ‚àò‚Çó f} (h : f = g.hom) :
    mkHom f hf = g := by ext : 1; exact h

lemma mkHom_ext_right {A B : Rep k G} (f : A ‚ü∂ B) (g : A ‚Üí‚Çó[k] B)
    {hg : ‚àÄ x, g ‚àò‚Çó A.œÅ x = B.œÅ x ‚àò‚Çó g} (h : f.hom = g) :
    f = mkHom g hg := by ext : 1; exact h

@[simps] def homLEquiv (A B : Rep k G) : (A ‚ü∂ B) ‚âÉ‚Çó[k] A.œÅ.hom B.œÅ where
  toFun := fun f => ‚ü®f.hom, f.comm‚ü©
  map_add' := fun x y => rfl
  map_smul' := fun r x => rfl
  invFun := fun f => ‚ü®f.hom, f.comm‚ü©
  left_inv := fun f => by ext; rfl
  right_inv := fun f => by ext; rfl

variable (k G)

/-- The trivial `k`-linear `G`-representation on a `k`-module `V.` -/
def trivial (V : Type u) [AddCommGroup V] [Module k V] : Rep k G :=
  Rep.of (@Representation.trivial k G V _ _ _ _)
set_option linter.uppercaseLean3 false in
#align Rep.trivial Rep.trivial

variable {k G}

theorem trivial_def {V : Type u} [AddCommGroup V] [Module k V] (g : G) (v : V) :
    (trivial k G V).œÅ g v = v :=
  rfl
set_option linter.uppercaseLean3 false in
#align Rep.trivial_def Rep.trivial_def

/-- A predicate for representations that fix every element. -/
abbrev IsTrivial (A : Rep k G) := A.œÅ.IsTrivial

instance {V : Type u} [AddCommGroup V] [Module k V] :
    IsTrivial (Rep.trivial k G V) where

instance {V : Type u} [AddCommGroup V] [Module k V] (œÅ : Representation k G V) [œÅ.IsTrivial] :
    IsTrivial (Rep.of œÅ) where

variable (k G)

@[simps] def trivialFunctor : ModuleCat k ‚•§ Rep k G where
  obj := fun A => Rep.trivial k G A
  map := fun f => Rep.mkHom f fun g => rfl

def forget‚ÇÇCompTrivialFunctorIso :
   trivialFunctor k G ‚ãô forget‚ÇÇ (Rep k G) (ModuleCat.{u} k) ‚âÖ ùü≠ (ModuleCat k) :=
Iso.refl _

-- Porting note: the two following instances were found automatically in mathlib3
noncomputable instance : PreservesLimits (forget‚ÇÇ (Rep k G) (ModuleCat.{u} k)) :=
  Action.instPreservesLimitsForget.{u} _ _

noncomputable instance : PreservesColimits (forget‚ÇÇ (Rep k G) (ModuleCat.{u} k)) :=
  Action.instPreservesColimitsForget.{u} _ _

instance : PreservesColimitsOfSize (trivialFunctor k G) :=
  Action.preservesColimitsOfSizeOfPreserves (trivialFunctor k G) <|
    show PreservesColimitsOfSize (ùü≠ (ModuleCat k)) by infer_instance

variable {k G}

/- Porting note: linter complains `simp` unfolds some types in the LHS, so
have removed `@[simp]`. -/
theorem MonoidalCategory.braiding_hom_apply {A B : Rep k G} (x : A) (y : B) :
    Action.Hom.hom (Œ≤_ A B).hom (TensorProduct.tmul k x y) = TensorProduct.tmul k y x :=
  rfl
set_option linter.uppercaseLean3 false in
#align Rep.monoidal_category.braiding_hom_apply Rep.MonoidalCategory.braiding_hom_apply

/- Porting note: linter complains `simp` unfolds some types in the LHS, so
have removed `@[simp]`. -/
theorem MonoidalCategory.braiding_inv_apply {A B : Rep k G} (x : A) (y : B) :
    Action.Hom.hom (Œ≤_ A B).inv (TensorProduct.tmul k y x) = TensorProduct.tmul k x y :=
  rfl
set_option linter.uppercaseLean3 false in
#align Rep.monoidal_category.braiding_inv_apply Rep.MonoidalCategory.braiding_inv_apply

section Linearization

variable (k G)

/-- The monoidal functor sending a type `H` with a `G`-action to the induced `k`-linear
`G`-representation on `k[H].` -/
noncomputable def linearization : MonoidalFunctor (Action (Type u) (MonCat.of G)) (Rep k G) :=
  (ModuleCat.monoidalFree k).mapAction (MonCat.of G)
set_option linter.uppercaseLean3 false in
#align Rep.linearization Rep.linearization

variable {k G}

@[simp]
theorem linearization_obj_œÅ (X : Action (Type u) (MonCat.of G)) (g : G) (x : X.V ‚Üí‚ÇÄ k) :
    ((linearization k G).obj X).œÅ g x = Finsupp.lmapDomain k k (X.œÅ g) x :=
  rfl
set_option linter.uppercaseLean3 false in
#align Rep.linearization_obj_œÅ Rep.linearization_obj_œÅ

theorem linearization_of (X : Action (Type u) (MonCat.of G)) (g : G) (x : X.V) :
    ((linearization k G).obj X).œÅ g (Finsupp.single x (1 : k))
      = Finsupp.single (X.œÅ g x) (1 : k) := by
  rw [linearization_obj_œÅ, Finsupp.lmapDomain_apply, Finsupp.mapDomain_single]
set_option linter.uppercaseLean3 false in
#align Rep.linearization_of Rep.linearization_of

-- Porting note: helps fixing `linearizationTrivialIso` since change in behaviour of ext
theorem linearization_single (X : Action (Type u) (MonCat.of G)) (g : G) (x : X.V) (r : k) :
    ((linearization k G).obj X).œÅ g (Finsupp.single x r) = Finsupp.single (X.œÅ g x) r :=
  by rw [linearization_obj_œÅ, Finsupp.lmapDomain_apply, Finsupp.mapDomain_single]

variable {X Y : Action (Type u) (MonCat.of G)} (f : X ‚ü∂ Y)

@[simp]
theorem linearization_map_hom : ((linearization k G).map f).hom = Finsupp.lmapDomain k k f.hom :=
  rfl
set_option linter.uppercaseLean3 false in
#align Rep.linearization_map_hom Rep.linearization_map_hom

theorem linearization_map_hom_single (x : X.V) (r : k) :
    ((linearization k G).map f).hom (Finsupp.single x r) = Finsupp.single (f.hom x) r :=
  Finsupp.mapDomain_single
set_option linter.uppercaseLean3 false in
#align Rep.linearization_map_hom_single Rep.linearization_map_hom_single

@[simp]
theorem linearization_Œº_hom (X Y : Action (Type u) (MonCat.of G)) :
    ((linearization k G).Œº X Y).hom = (finsuppTensorFinsupp' k X.V Y.V).toLinearMap :=
  rfl
set_option linter.uppercaseLean3 false in
#align Rep.linearization_Œº_hom Rep.linearization_Œº_hom

@[simp]
theorem linearization_Œº_inv_hom (X Y : Action (Type u) (MonCat.of G)) :
    (inv ((linearization k G).Œº X Y)).hom = (finsuppTensorFinsupp' k X.V Y.V).symm.toLinearMap := by
-- Porting note: broken proof was
/-simp_rw [‚Üê Action.forget_map, Functor.map_inv, Action.forget_map, linearization_Œº_hom]
  apply IsIso.inv_eq_of_hom_inv_id _
  exact LinearMap.ext fun x => LinearEquiv.symm_apply_apply _ _-/
  rw [‚Üê Action.forget_map, Functor.map_inv]
  apply IsIso.inv_eq_of_hom_inv_id
  exact LinearMap.ext fun x => LinearEquiv.symm_apply_apply (finsuppTensorFinsupp' k X.V Y.V) x
set_option linter.uppercaseLean3 false in
#align Rep.linearization_Œº_inv_hom Rep.linearization_Œº_inv_hom

@[simp]
theorem linearization_Œµ_hom : (linearization k G).Œµ.hom = Finsupp.lsingle PUnit.unit :=
  rfl
set_option linter.uppercaseLean3 false in
#align Rep.linearization_Œµ_hom Rep.linearization_Œµ_hom

-- This was always a bad simp lemma, but the linter did not notice until lean4#2644
@[simp, nolint simpNF]
theorem linearization_Œµ_inv_hom_apply (r : k) :
    (inv (linearization k G).Œµ).hom (Finsupp.single PUnit.unit r) = r :=
  IsIso.hom_inv_id_apply (linearization k G).Œµ r
set_option linter.uppercaseLean3 false in
#align Rep.linearization_Œµ_inv_hom_apply Rep.linearization_Œµ_inv_hom_apply

variable (k G)

/-- The linearization of a type `X` on which `G` acts trivially is the trivial `G`-representation
on `k[X]`. -/
@[simps!]
noncomputable def linearizationTrivialIso (X : Type u) :
    (linearization k G).obj (Action.mk X 1) ‚âÖ trivial k G (X ‚Üí‚ÇÄ k) :=
  Action.mkIso (Iso.refl _) fun _ => Finsupp.lhom_ext' fun _ => LinearMap.ext
    fun _ => linearization_single ..
set_option linter.uppercaseLean3 false in
#align Rep.linearization_trivial_iso Rep.linearizationTrivialIso

/-- Given a `G`-action on `H`, this is `k[H]` bundled with the natural representation
`G ‚Üí* End(k[H])` as a term of type `Rep k G`. -/
noncomputable abbrev ofMulAction (H : Type u) [MulAction G H] : Rep k G :=
  of <| Representation.ofMulAction k G H
set_option linter.uppercaseLean3 false in
#align Rep.of_mul_action Rep.ofMulAction

/-- The `k`-linear `G`-representation on `k[G]`, induced by left multiplication. -/
noncomputable def leftRegular : Rep k G :=
  ofMulAction k G G
set_option linter.uppercaseLean3 false in
#align Rep.left_regular Rep.leftRegular

/-- The `k`-linear `G`-representation on `k[G‚Åø]`, induced by left multiplication. -/
noncomputable def diagonal (n : ‚Ñï) : Rep k G :=
  ofMulAction k G (Fin n ‚Üí G)
set_option linter.uppercaseLean3 false in
#align Rep.diagonal Rep.diagonal

/-- The linearization of a type `H` with a `G`-action is definitionally isomorphic to the
`k`-linear `G`-representation on `k[H]` induced by the `G`-action on `H`. -/
noncomputable def linearizationOfMulActionIso (H : Type u) [MulAction G H] :
    (linearization k G).obj (Action.ofMulAction G H) ‚âÖ ofMulAction k G H :=
  Iso.refl _
set_option linter.uppercaseLean3 false in
#align Rep.linearization_of_mul_action_iso Rep.linearizationOfMulActionIso

section

variable (k G A : Type) [CommRing k] [Monoid G] [AddCommGroup A]
  [Module k A] [DistribMulAction G A] [SMulCommClass G k A]

/-- Turns a `k`-module `A` with a compatible `DistribMulAction` of a monoid `G` into a
`k`-linear `G`-representation on `A`. -/
def ofDistribMulAction : Rep k G := Rep.of (Representation.ofDistribMulAction k G A)

@[simp] theorem ofDistribMulAction_œÅ_apply_apply (g : G) (a : A) :
    (ofDistribMulAction k G A).œÅ g a = g ‚Ä¢ a := rfl

/-- Given an `R`-algebra `S`, the `‚Ñ§`-linear representation associated to the natural action of
`S ‚âÉ‚Çê[R] S` on `S`. -/
@[simp] def ofAlgebraAut (R S : Type) [CommRing R] [CommRing S] [Algebra R S] :
    Rep ‚Ñ§ (S ‚âÉ‚Çê[R] S) := ofDistribMulAction ‚Ñ§ (S ‚âÉ‚Çê[R] S) S

end
section
variable (M G : Type) [Monoid M] [CommGroup G] [MulDistribMulAction M G]

/-- Turns a `CommGroup` `G` with a `MulDistribMulAction` of a monoid `M` into a
`‚Ñ§`-linear `M`-representation on `Additive G`. -/
def ofMulDistribMulAction : Rep ‚Ñ§ M := Rep.of (Representation.ofMulDistribMulAction M G)

@[simp] theorem ofMulDistribMulAction_œÅ_apply_apply (g : M) (a : G) :
    (ofMulDistribMulAction M G).œÅ g a = g ‚Ä¢ a := rfl

/-- Given an `R`-algebra `S`, the `‚Ñ§`-linear representation associated to the natural action of
`S ‚âÉ‚Çê[R] S` on `SÀ£`. -/
@[simp] def ofAlgebraAutOnUnits (R S : Type) [CommRing R] [CommRing S] [Algebra R S] :
    Rep ‚Ñ§ (S ‚âÉ‚Çê[R] S) := Rep.ofMulDistribMulAction (S ‚âÉ‚Çê[R] S) SÀ£

end

variable {k G}

/-- Given an element `x : A`, there is a natural morphism of representations `k[G] ‚ü∂ A` sending
`g ‚Ü¶ A.œÅ(g)(x).` -/
def leftRegularHom (A : Rep k G) (x : A) : Rep.ofMulAction k G G ‚ü∂ A :=
  mkHom (Finsupp.lift _ _ _ fun g => A.œÅ g x) <| fun g => Finsupp.lhom_ext' fun y =>
    LinearMap.ext_ring <| by
      simp only [coe_of, of_œÅ, LinearMap.coe_comp, Function.comp_apply, Finsupp.lsingle_apply,
        Representation.ofMulAction_single, smul_eq_mul, Finsupp.lift_apply, map_mul,
        LinearMap.mul_apply, zero_smul, Finsupp.sum_single_index, one_smul]
set_option linter.uppercaseLean3 false in
#align Rep.left_regular_hom Rep.leftRegularHom

@[simp] theorem leftRegularHom_single {A : Rep k G} (x : A) (g : G) (r : k) :
    (leftRegularHom A x).hom (Finsupp.single g r) = r ‚Ä¢ A.œÅ g x := by
  simp only [leftRegularHom, mkHom_hom_apply, coe_of, Finsupp.lift_apply, zero_smul,
    Finsupp.sum_single_index]

theorem leftRegularHom_one {A : Rep k G} (x : A) :
    (leftRegularHom A x).hom (Finsupp.single 1 1) = x := by
  simp only [leftRegularHom_single, map_one, LinearMap.one_apply, one_smul]

/-- Given a `k`-linear `G`-representation `A`, there is a `k`-linear isomorphism between
representation morphisms `Hom(k[G], A)` and `A`. -/
@[simps]
noncomputable def leftRegularHomEquiv (A : Rep k G) : (Rep.ofMulAction k G G ‚ü∂ A) ‚âÉ‚Çó[k] A where
  toFun f := f.hom (Finsupp.single 1 1)
  map_add' x y := rfl
  map_smul' r x := rfl
  invFun x := leftRegularHom A x
  left_inv f := by
    refine' mkHom_ext_left _ _ (Finsupp.lhom_ext' fun x : G => LinearMap.ext_ring _)
    simp only [‚Üê hom_comm_apply, coe_of, of_œÅ, Representation.ofMulAction_single, smul_eq_mul,
      mul_one, LinearMap.coe_comp, Function.comp_apply, Finsupp.lsingle_apply, Finsupp.lift_apply,
      zero_smul, Finsupp.sum_single_index, one_smul]
    rfl
  right_inv x := leftRegularHom_one x
set_option linter.uppercaseLean3 false in
#align Rep.left_regular_hom_equiv Rep.leftRegularHomEquiv

theorem leftRegularHomEquiv_symm_single {A : Rep k G} (x : A) (g : G) :
    ((leftRegularHomEquiv A).symm x).hom (Finsupp.single g 1) = A.œÅ g x := by
  simp only [leftRegularHomEquiv_symm_apply, leftRegularHom_single, one_smul]
set_option linter.uppercaseLean3 false in
#align Rep.left_regular_hom_equiv_symm_single Rep.leftRegularHomEquiv_symm_single

end Linearization

end

section MonoidalClosed
open MonoidalCategory Action

variable [Group G] (A B C : Rep k G)

/-- Given a `k`-linear `G`-representation `(A, œÅ‚ÇÅ)`, this is the 'internal Hom' functor sending
`(B, œÅ‚ÇÇ)` to the representation `Hom‚Çñ(A, B)` that maps `g : G` and `f : A ‚Üí‚Çó[k] B` to
`(œÅ‚ÇÇ g) ‚àò‚Çó f ‚àò‚Çó (œÅ‚ÇÅ g‚Åª¬π)`. -/
@[simps]
protected def ihom (A : Rep k G) : Rep k G ‚•§ Rep k G where
  obj B := Rep.of (Representation.linHom A.œÅ B.œÅ)
  map := fun {X} {Y} f =>
    { hom := ModuleCat.ofHom (LinearMap.llcomp k _ _ _ f.hom)
      comm := fun g => LinearMap.ext fun x => LinearMap.ext fun y => by
        show f.hom (X.œÅ g _) = _
        simp only [hom_comm_apply]; rfl }
  map_id := fun _ => by ext; rfl
  map_comp := fun _ _ => by ext; rfl
set_option linter.uppercaseLean3 false in
#align Rep.ihom Rep.ihom

@[simp] theorem ihom_obj_œÅ_apply {A B : Rep k G} (g : G) (x : A ‚Üí‚Çó[k] B) :
    ((Rep.ihom A).obj B).œÅ g x = B.œÅ g ‚àò‚Çó x ‚àò‚Çó A.œÅ g‚Åª¬π :=
  rfl
set_option linter.uppercaseLean3 false in
#align Rep.ihom_obj_œÅ_apply Rep.ihom_obj_œÅ_apply

/-- Given a `k`-linear `G`-representation `A`, this is the Hom-set bijection in the adjunction
`A ‚äó - ‚ä£ ihom(A, -)`. It sends `f : A ‚äó B ‚ü∂ C` to a `Rep k G` morphism defined by currying the
`k`-linear map underlying `f`, giving a map `A ‚Üí‚Çó[k] B ‚Üí‚Çó[k] C`, then flipping the arguments. -/
def homEquiv (A B C : Rep k G) : (A ‚äó B ‚ü∂ C) ‚âÉ (B ‚ü∂ (Rep.ihom A).obj C) :=
  (Rep.homLEquiv (A ‚äó B) C).toEquiv.trans ((A.œÅ.homEquiv B.œÅ C.œÅ).trans
    (Rep.homLEquiv B ((Rep.ihom A).obj C)).toEquiv.symm)

variable {A B C}

/-- Porting note: if we generate this with `@[simps]` the linter complains some types in the LHS
simplify. -/
theorem homEquiv_apply_hom (f : A ‚äó B ‚ü∂ C) :
    (homEquiv A B C f).hom = (TensorProduct.curry f.hom).flip := rfl
set_option linter.uppercaseLean3 false in
#align Rep.hom_equiv_apply_hom Rep.homEquiv_apply_hom

/-- Porting note: if we generate this with `@[simps]` the linter complains some types in the LHS
simplify. -/
theorem homEquiv_symm_apply_hom (f : B ‚ü∂ (Rep.ihom A).obj C) :
    ((homEquiv A B C).symm f).hom = TensorProduct.uncurry k A B C f.hom.flip := rfl
set_option linter.uppercaseLean3 false in
#align Rep.hom_equiv_symm_apply_hom Rep.homEquiv_symm_apply_hom

instance : MonoidalClosed (Rep k G) where
  closed := fun A =>
  { isAdj :=
    { right := Rep.ihom A
      adj := Adjunction.mkOfHomEquiv (
      { homEquiv := Rep.homEquiv A
        homEquiv_naturality_left_symm := fun _ _ => Action.Hom.ext _ _
          (TensorProduct.ext' fun _ _ => rfl)
        homEquiv_naturality_right := fun _ _ => Action.Hom.ext _ _ (LinearMap.ext
          fun _ => LinearMap.ext fun _ => rfl) })}}

@[simp]
theorem ihom_obj_œÅ_def (A B : Rep k G) : ((ihom A).obj B).œÅ = ((Rep.ihom A).obj B).œÅ :=
  rfl
set_option linter.uppercaseLean3 false in
#align Rep.ihom_obj_œÅ_def Rep.ihom_obj_œÅ_def

@[simp]
theorem homEquiv_def (A B C : Rep k G) : (ihom.adjunction A).homEquiv B C = Rep.homEquiv A B C :=
  rfl
set_option linter.uppercaseLean3 false in
#align Rep.hom_equiv_def Rep.homEquiv_def

@[simp]
theorem ihom_ev_app_hom (A B : Rep k G) :
    Action.Hom.hom ((ihom.ev A).app B)
      = TensorProduct.uncurry k A (A ‚Üí‚Çó[k] B) B LinearMap.id.flip := by
  ext; rfl
set_option linter.uppercaseLean3 false in
#align Rep.ihom_ev_app_hom Rep.ihom_ev_app_hom

@[simp] theorem ihom_coev_app_hom (A B : Rep k G) :
    Action.Hom.hom ((ihom.coev A).app B) = (TensorProduct.mk k _ _).flip :=
  LinearMap.ext fun _ => LinearMap.ext fun _ => rfl
set_option linter.uppercaseLean3 false in
#align Rep.ihom_coev_app_hom Rep.ihom_coev_app_hom

variable (A B C)

/-- There is a `k`-linear isomorphism between the sets of representation morphisms`Hom(A ‚äó B, C)`
and `Hom(B, Hom‚Çñ(A, C))`. -/
def MonoidalClosed.linearHomEquiv : (A ‚äó B ‚ü∂ C) ‚âÉ‚Çó[k] B ‚ü∂ A ‚ü∂[Rep k G] C :=
  { (ihom.adjunction A).homEquiv _ _ with
    map_add' := fun _ _ => rfl
    map_smul' := fun _ _ => rfl }
  set_option linter.uppercaseLean3 false in
#align Rep.monoidal_closed.linear_hom_equiv Rep.MonoidalClosed.linearHomEquiv

/-- There is a `k`-linear isomorphism between the sets of representation morphisms`Hom(A ‚äó B, C)`
and `Hom(A, Hom‚Çñ(B, C))`. -/
def MonoidalClosed.linearHomEquivComm : (A ‚äó B ‚ü∂ C) ‚âÉ‚Çó[k] A ‚ü∂ B ‚ü∂[Rep k G] C :=
  Linear.homCongr k (Œ≤_ A B) (Iso.refl _) ‚â™‚â´‚Çó MonoidalClosed.linearHomEquiv _ _ _
set_option linter.uppercaseLean3 false in
#align Rep.monoidal_closed.linear_hom_equiv_comm Rep.MonoidalClosed.linearHomEquivComm

variable {A B C}

-- `simpNF` times out
@[simp, nolint simpNF]
theorem MonoidalClosed.linearHomEquiv_hom (f : A ‚äó B ‚ü∂ C) :
    (MonoidalClosed.linearHomEquiv A B C f).hom = (TensorProduct.curry f.hom).flip :=
  rfl
set_option linter.uppercaseLean3 false in
#align Rep.monoidal_closed.linear_hom_equiv_hom Rep.MonoidalClosed.linearHomEquiv_hom

-- `simpNF` times out
@[simp, nolint simpNF]
theorem MonoidalClosed.linearHomEquivComm_hom (f : A ‚äó B ‚ü∂ C) :
    (MonoidalClosed.linearHomEquivComm A B C f).hom = TensorProduct.curry f.hom :=
  rfl
set_option linter.uppercaseLean3 false in
#align Rep.monoidal_closed.linear_hom_equiv_comm_hom Rep.MonoidalClosed.linearHomEquivComm_hom

-- This was always a bad simp lemma, but the linter did not notice until lean4#2644
@[simp, nolint simpNF]
theorem MonoidalClosed.linearHomEquiv_symm_hom (f : B ‚ü∂ A ‚ü∂[Rep k G] C) :
    ((MonoidalClosed.linearHomEquiv A B C).symm f).hom = TensorProduct.uncurry k A B C f.hom.flip :=
  rfl
set_option linter.uppercaseLean3 false in
#align Rep.monoidal_closed.linear_hom_equiv_symm_hom Rep.MonoidalClosed.linearHomEquiv_symm_hom

-- This was always a bad simp lemma, but the linter did not notice until lean4#2644
@[simp, nolint simpNF]
theorem MonoidalClosed.linearHomEquivComm_symm_hom (f : A ‚ü∂ B ‚ü∂[Rep k G] C) :
    ((MonoidalClosed.linearHomEquivComm A B C).symm f).hom
      = TensorProduct.uncurry k A B C f.hom :=
  TensorProduct.ext' fun _ _ => rfl
set_option linter.uppercaseLean3 false in
#align Rep.monoidal_closed.linear_hom_equiv_comm_symm_hom Rep.MonoidalClosed.linearHomEquivComm_symm_hom

end MonoidalClosed

end Rep

namespace Representation
open MonoidalCategory
variable {k G : Type u} [CommRing k] [Monoid G] {V W : Type u} [AddCommGroup V] [AddCommGroup W]
  [Module k V] [Module k W] (œÅ : Representation k G V) (œÑ : Representation k G W)

/-- Tautological isomorphism to help Lean in typechecking. -/
def repOfTprodIso : Rep.of (œÅ.tprod œÑ) ‚âÖ Rep.of œÅ ‚äó Rep.of œÑ :=
  Iso.refl _
set_option linter.uppercaseLean3 false in
#align representation.Rep_of_tprod_iso Representation.repOfTprodIso

theorem repOfTprodIso_apply (x : TensorProduct k V W) : (repOfTprodIso œÅ œÑ).hom.hom x = x :=
  rfl
set_option linter.uppercaseLean3 false in
#align representation.Rep_of_tprod_iso_apply Representation.repOfTprodIso_apply

theorem repOfTprodIso_inv_apply (x : TensorProduct k V W) : (repOfTprodIso œÅ œÑ).inv.hom x = x :=
  rfl
set_option linter.uppercaseLean3 false in
#align representation.Rep_of_tprod_iso_inv_apply Representation.repOfTprodIso_inv_apply

end Representation

/-!
# The categorical equivalence `Rep k G ‚âå Module.{u} (MonoidAlgebra k G)`.
-/


namespace Rep

variable {k G : Type u} [CommRing k] [Monoid G]

-- Verify that the symmetric monoidal structure is available.
example : SymmetricCategory (Rep k G) := by infer_instance

example : MonoidalPreadditive (Rep k G) := by infer_instance

example : MonoidalLinear k (Rep k G) := by infer_instance

noncomputable section

/-- Auxiliary lemma for `toModuleMonoidAlgebra`. -/
theorem to_Module_monoidAlgebra_map_aux {k G : Type*} [CommRing k] [Monoid G] (V W : Type*)
    [AddCommGroup V] [AddCommGroup W] [Module k V] [Module k W] (œÅ : G ‚Üí* V ‚Üí‚Çó[k] V)
    (œÉ : G ‚Üí* W ‚Üí‚Çó[k] W) (f : V ‚Üí‚Çó[k] W) (w : ‚àÄ g : G, f.comp (œÅ g) = (œÉ g).comp f)
    (r : MonoidAlgebra k G) (x : V) :
    f ((((MonoidAlgebra.lift k G (V ‚Üí‚Çó[k] V)) œÅ) r) x) =
      (((MonoidAlgebra.lift k G (W ‚Üí‚Çó[k] W)) œÉ) r) (f x) := by
  apply MonoidAlgebra.induction_on r
  ¬∑ intro g
    simp only [one_smul, MonoidAlgebra.lift_single, MonoidAlgebra.of_apply]
    exact LinearMap.congr_fun (w g) x
  ¬∑ intro g h gw hw; simp only [map_add, add_left_inj, LinearMap.add_apply, hw, gw]
  ¬∑ intro r g w
    simp only [AlgHom.map_smul, w, RingHom.id_apply, LinearMap.smul_apply, LinearMap.map_smul‚Çõ‚Çó]
set_option linter.uppercaseLean3 false in
#align Rep.to_Module_monoid_algebra_map_aux Rep.to_Module_monoidAlgebra_map_aux

/-- Auxiliary definition for `toModuleMonoidAlgebra`. -/
def toModuleMonoidAlgebraMap {V W : Rep k G} (f : V ‚ü∂ W) :
    ModuleCat.of (MonoidAlgebra k G) V.œÅ.asModule ‚ü∂ ModuleCat.of (MonoidAlgebra k G) W.œÅ.asModule :=
  { f.hom with
    map_smul' := fun r x => to_Module_monoidAlgebra_map_aux V.V W.V V.œÅ W.œÅ f.hom f.comm r x }
set_option linter.uppercaseLean3 false in
#align Rep.to_Module_monoid_algebra_map Rep.toModuleMonoidAlgebraMap

/-- Functorially convert a representation of `G` into a module over `MonoidAlgebra k G`. -/
def toModuleMonoidAlgebra : Rep k G ‚•§ ModuleCat.{u} (MonoidAlgebra k G) where
  obj V := ModuleCat.of _ V.œÅ.asModule
  map f := toModuleMonoidAlgebraMap f
set_option linter.uppercaseLean3 false in
#align Rep.to_Module_monoid_algebra Rep.toModuleMonoidAlgebra

/-- Functorially convert a module over `MonoidAlgebra k G` into a representation of `G`. -/
def ofModuleMonoidAlgebra : ModuleCat.{u} (MonoidAlgebra k G) ‚•§ Rep k G where
  obj M := Rep.of (Representation.ofModule M)
  map f :=
    { hom := { f with map_smul' := fun r x => f.map_smul (algebraMap k _ r) x }
      comm := fun g => by ext; apply f.map_smul }
set_option linter.uppercaseLean3 false in
#align Rep.of_Module_monoid_algebra Rep.ofModuleMonoidAlgebra

theorem ofModuleMonoidAlgebra_obj_coe (M : ModuleCat.{u} (MonoidAlgebra k G)) :
    (ofModuleMonoidAlgebra.obj M : Type u) = RestrictScalars k (MonoidAlgebra k G) M :=
  rfl
set_option linter.uppercaseLean3 false in
#align Rep.of_Module_monoid_algebra_obj_coe Rep.ofModuleMonoidAlgebra_obj_coe

theorem ofModuleMonoidAlgebra_obj_œÅ (M : ModuleCat.{u} (MonoidAlgebra k G)) :
    (ofModuleMonoidAlgebra.obj M).œÅ = Representation.ofModule M :=
  rfl
set_option linter.uppercaseLean3 false in
#align Rep.of_Module_monoid_algebra_obj_œÅ Rep.ofModuleMonoidAlgebra_obj_œÅ

/-- Auxiliary definition for `equivalenceModuleMonoidAlgebra`. -/
def counitIsoAddEquiv {M : ModuleCat.{u} (MonoidAlgebra k G)} :
    (ofModuleMonoidAlgebra ‚ãô toModuleMonoidAlgebra).obj M ‚âÉ+ M := by
  dsimp [ofModuleMonoidAlgebra, toModuleMonoidAlgebra]
  refine' (Representation.ofModule M).asModuleEquiv.trans
    (RestrictScalars.addEquiv k (MonoidAlgebra k G) _)
set_option linter.uppercaseLean3 false in
#align Rep.counit_iso_add_equiv Rep.counitIsoAddEquiv

/-- Auxiliary definition for `equivalenceModuleMonoidAlgebra`. -/
def unitIsoAddEquiv {V : Rep k G} : V ‚âÉ+ (toModuleMonoidAlgebra ‚ãô ofModuleMonoidAlgebra).obj V := by
  dsimp [ofModuleMonoidAlgebra, toModuleMonoidAlgebra]
  refine' V.œÅ.asModuleEquiv.symm.trans _
  exact (RestrictScalars.addEquiv _ _ _).symm
set_option linter.uppercaseLean3 false in
#align Rep.unit_iso_add_equiv Rep.unitIsoAddEquiv

/-- Auxiliary definition for `equivalenceModuleMonoidAlgebra`. -/
def counitIso (M : ModuleCat.{u} (MonoidAlgebra k G)) :
    (ofModuleMonoidAlgebra ‚ãô toModuleMonoidAlgebra).obj M ‚âÖ M :=
  LinearEquiv.toModuleIso'
    { counitIsoAddEquiv with
      map_smul' := fun r x => by
        dsimp [counitIsoAddEquiv]
/- Porting note: rest of broken proof was `simp`. -/
        rw [AddEquiv.trans_apply]
        rw [AddEquiv.trans_apply]
        erw [@Representation.ofModule_asAlgebraHom_apply_apply k G _ _ _ _ (_)]
        exact AddEquiv.symm_apply_apply _ _}
set_option linter.uppercaseLean3 false in
#align Rep.counit_iso Rep.counitIso

theorem unit_iso_comm (V : Rep k G) (g : G) (x : V) :
    unitIsoAddEquiv ((V.œÅ g).toFun x) = ((ofModuleMonoidAlgebra.obj
      (toModuleMonoidAlgebra.obj V)).œÅ g).toFun (unitIsoAddEquiv x) := by
  dsimp [unitIsoAddEquiv, ofModuleMonoidAlgebra, toModuleMonoidAlgebra]
/- Porting note: rest of broken proof was
  simp only [AddEquiv.apply_eq_iff_eq, AddEquiv.apply_symm_apply,
    Representation.asModuleEquiv_symm_map_rho, Representation.ofModule_asModule_act] -/
  erw [Representation.asModuleEquiv_symm_map_rho]
  rfl
set_option linter.uppercaseLean3 false in
#align Rep.unit_iso_comm Rep.unit_iso_comm

/-- Auxiliary definition for `equivalenceModuleMonoidAlgebra`. -/
def unitIso (V : Rep k G) : V ‚âÖ (toModuleMonoidAlgebra ‚ãô ofModuleMonoidAlgebra).obj V :=
  Action.mkIso
    (LinearEquiv.toModuleIso'
      { unitIsoAddEquiv with
        map_smul' := fun r x => by
          dsimp [unitIsoAddEquiv]
/- Porting note: rest of broken proof was
          simp only [Representation.asModuleEquiv_symm_map_smul,
            RestrictScalars.addEquiv_symm_map_algebraMap_smul] -/
          -- This used to be `rw`, but we need `erw` after leanprover/lean4#2644
          erw [AddEquiv.trans_apply,
            Representation.asModuleEquiv_symm_map_smul]
          rfl })
    fun g => by ext; apply unit_iso_comm
set_option linter.uppercaseLean3 false in
#align Rep.unit_iso Rep.unitIso

/-- The categorical equivalence `Rep k G ‚âå ModuleCat (MonoidAlgebra k G)`. -/
def equivalenceModuleMonoidAlgebra : Rep k G ‚âå ModuleCat.{u} (MonoidAlgebra k G) where
  functor := toModuleMonoidAlgebra
  inverse := ofModuleMonoidAlgebra
  unitIso := NatIso.ofComponents (fun V => unitIso V) (by aesop_cat)
  counitIso := NatIso.ofComponents (fun M => counitIso M) (by aesop_cat)
set_option linter.uppercaseLean3 false in
#align Rep.equivalence_Module_monoid_algebra Rep.equivalenceModuleMonoidAlgebra

-- TODO Verify that the equivalence with `ModuleCat (MonoidAlgebra k G)` is a monoidal functor.
end
