/-
Copyright (c) 2025 Sophie Morel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sophie Morel
-/
import Mathlib.Algebra.Category.FGModuleCat.Abelian
import Mathlib.Algebra.Category.ModuleCat.Injective
import Mathlib.RepresentationTheory.Character
import Mathlib.RepresentationTheory.Maschke
import Mathlib.RingTheory.SimpleModule.InjectiveProjective

/-!
# Applications of Maschke's theorem

This proves some properties of representations that follow from Maschke's
theorem. In particular, we assume that the group is finite, and we also assume
that the base ring is a field of characteristic zero (or at least a field where
the order of the group is invertible).

In particular, we prove that, if `G` is a finite group whose order is invertible
in a field `k`, then every object of `Rep k G` (resp. `FDRep k G`) is injective and
projective.

We also give two simpleness criteria for an object `V` of `FDRep k G`, when `k` is
an algebraically closed field in which the order of `G` is invertible:
* `FDRep.simple_iff_end_is_rank_one`: `V` is simple if and only `V ‚ü∂ V` is a `k`-vector
space of dimension `1`.
* `FDRep.simple_iff_char_is_norm_one`: when `k` is characteristic zero, `V` is simple
if and only if `‚àë g : G, V.character g * V.character g‚Åª¬π = Fintype.card G`.

-/

universe u

variable {k : Type u} [Field k] {G : Type u} [Fintype G] [Group G]

open CategoryTheory Limits

namespace Rep

variable [NeZero (Fintype.card G : k)]

/--
If `G` is finite and its order is nonzero in the field `k`, then every object of
`Rep k G` is injective.
-/
instance (V : Rep k G) : Injective V := by
  rw [‚Üê Rep.equivalenceModuleMonoidAlgebra.map_injective_iff,
    ‚Üê Module.injective_iff_injective_object]
  exact Module.injective_of_semisimple_ring _ _

/--
If `G` is finite and its order is nonzero in the field `k`, then every object of
`Rep k G` is projective.
-/
-- Will this clash with the previously defined `Projective` instances?
instance (V : Rep k G) : Projective V := by
  rw [‚Üê Rep.equivalenceModuleMonoidAlgebra.map_projective_iff,
    ‚Üê IsProjective.iff_projective]
  exact Module.projective_of_semisimple_ring _ _

end Rep

namespace FDRep

variable [NeZero (Fintype.card G : k)]

/--
If `G` is finite and its order is nonzero in the field `k`, then every object of
`FDRep k G` is injective.
-/
instance (V : FDRep k G) : Injective V := (forget‚ÇÇ (FDRep k G)
  (Rep k G)).injective_of_map_injective _ inferInstance

/--
If `G` is finite and its order is nonzero in the field `k`, then every object of
`FDRep k G` is projective.
-/
instance (V : FDRep k G) : Projective V := (forget‚ÇÇ (FDRep k G)
  (Rep k G)).projective_of_map_projective _ inferInstance

variable [IsAlgClosed k]

/--
If `G` is finite and its order is nonzero in an algebraically closed field `k`,
then an object of `FDRep k G` is simple if and only if its space of endomorphisms is
a `k`-vector space of dimension `1`.
-/
lemma simple_iff_end_is_rank_one (V : FDRep k G) :
    Simple V ‚Üî Module.finrank k (V ‚ü∂ V) = 1 where
  mp h := by
    rw [finrank_hom_simple_simple_eq_one_iff]
    exact Nonempty.intro (Iso.refl _)
  mpr h := by
    refine {mono_isIso_iff_nonzero f _ := ‚ü®fun hf habs ‚Ü¶ ?_, fun hf ‚Ü¶ ?_‚ü©}
    ¬∑ rw [habs] at hf
      obtain ‚ü®g, hg‚ü© := (Module.finrank_pos_iff_exists_ne_zero (R := k) (M := V ‚ü∂ V)).mp
        (by rw [h]; exact zero_lt_one)
      exact hg (Limits.IsZero.eq_zero_of_src (IsZero.of_iso (isZero_zero _)
        ((isIsoZeroEquivIsoZero _ _).toFun hf).2) g)
    ¬∑ have : Epi f := by
        have : Epi (Abelian.image.Œπ f) := by
          have h‚ÇÅ := Injective.comp_factorThru (ùüô _) (Abelian.image.Œπ f)
          have h‚ÇÇ : Injective.factorThru (ùüô _) (Abelian.image.Œπ f) ‚â´ Abelian.image.Œπ f ‚â† 0 := by
            intro habs
            apply_fun (fun x ‚Ü¶ x ‚â´ Abelian.image.Œπ f) at h‚ÇÅ
            rw [Category.id_comp, Category.assoc, habs, Limits.comp_zero] at h‚ÇÅ
            rw [‚Üê Abelian.image.fac f, ‚Üê h‚ÇÅ, Limits.comp_zero] at hf
            exact hf (Eq.refl _)
          obtain ‚ü®c, hc‚ü© := (finrank_eq_one_iff_of_nonzero' _ h‚ÇÇ).mp h (ùüô V)
          refine Preadditive.epi_of_cancel_zero _ (fun g hg ‚Ü¶ ?_)
          apply_fun (fun x ‚Ü¶ x ‚â´ g) at hc
          simp only [equalizer_as_kernel, Linear.smul_comp, Category.assoc, hg, Limits.comp_zero,
            smul_zero, Category.id_comp] at hc
          exact hc.symm
        rw [‚Üê Abelian.image.fac f]
        exact epi_comp _ _
      exact isIso_of_mono_of_epi f

lemma simple_iff_char_is_norm_one [CharZero k] (V : FDRep k G) :
    Simple V ‚Üî ‚àë g : G, V.character g * V.character g‚Åª¬π = Fintype.card G where
  mp h := by
    have := invertibleOfNonzero (NeZero.ne (Fintype.card G : k))
    have := char_orthonormal V V
    simp only [Nonempty.intro (Iso.refl V), ‚ÜìreduceIte] at this
    apply_fun (fun x ‚Ü¶ x * (Fintype.card G : k)) at this
    rw [mul_comm, ‚Üê smul_eq_mul, smul_smul, mul_invOf_self] at this
    simp only [smul_eq_mul, one_mul] at this
    exact this
  mpr h := by
    have := invertibleOfNonzero (NeZero.ne (Fintype.card G : k))
    have eq := FDRep.scalar_product_char_eq_finrank_equivariant V V
    rw [h] at eq
    simp only [invOf_eq_inv, smul_eq_mul, inv_mul_cancel_of_invertible] at eq
    rw [simple_iff_end_is_rank_one, ‚Üê Nat.cast_inj (R := k), ‚Üê eq, Nat.cast_one]

end FDRep
