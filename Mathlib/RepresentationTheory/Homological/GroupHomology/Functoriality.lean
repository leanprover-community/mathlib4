/-
Copyright (c) 2025 Amelia Livingston. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Amelia Livingston
-/
import Mathlib.RepresentationTheory.Homological.GroupHomology.LowDegree

/-!
# Functoriality of group homology

Given a commutative ring `k`, a group homomorphism `f : G â†’* H`, a `k`-linear `G`-representation
`A`, a `k`-linear `H`-representation `B`, and a representation morphism `A âŸ¶ Res(f)(B)`, we get
a chain map `inhomogeneousChains A âŸ¶ inhomogeneousChains B` and hence maps on homology
`Hâ‚™(G, A) âŸ¶ Hâ‚™(H, B)`.

We also provide extra API for these maps in degrees 0, 1, 2.

## Main definitions

* `groupHomology.chainsMap f Ï†` is the map `inhomogeneousChains A âŸ¶ inhomogeneousChains B`
  induced by a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`.
* `groupHomology.map f Ï† n` is the map `Hâ‚™(G, A) âŸ¶ Hâ‚™(H, B)` induced by a group homomorphism
  `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`.

-/

universe v u

namespace groupHomology

open CategoryTheory Rep Finsupp Representation

variable {k G H : Type u} [CommRing k] [Group G] [Group H]
  {A : Rep k G} {B : Rep k H} (f : G â†’* H) (Ï† : A âŸ¶ (Action.res _ f).obj B) (n : â„•)

theorem congr {fâ‚ fâ‚‚ : G â†’* H} (h : fâ‚ = fâ‚‚) {Ï† : A âŸ¶ (Action.res _ fâ‚).obj B} {T : Type*}
    (F : (f : G â†’* H) â†’ (Ï† : A âŸ¶ (Action.res _ f).obj B) â†’ T) :
    F fâ‚ Ï† = F fâ‚‚ (h â–¸ Ï†) := by
  subst h
  rfl

variable [DecidableEq G] [DecidableEq H]

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the chain map sending `âˆ‘ aáµ¢Â·gáµ¢ : Gâ¿ â†’â‚€ A` to `âˆ‘ Ï†(aáµ¢)Â·(f âˆ˜ gáµ¢) : Hâ¿ â†’â‚€ B`. -/
@[simps! (config := .lemmasOnly) f f_hom]
noncomputable def chainsMap :
    inhomogeneousChains A âŸ¶ inhomogeneousChains B where
  f i := ModuleCat.ofHom <| mapRange.linearMap Ï†.hom.hom âˆ˜â‚— lmapDomain A k (f âˆ˜ Â·)
  comm' i j (hij : _ = _) := by
    subst hij
    refine ModuleCat.hom_ext <| lhom_ext fun _ _ => ?_
    simpa [Fin.comp_contractNth, map_add] using congr(single _ $((hom_comm_apply Ï† (_)â»Â¹ _).symm))

@[reassoc (attr := simp)]
lemma lsingle_comp_chainsMap_f (n : â„•) (x : Fin n â†’ G) :
    ModuleCat.ofHom (lsingle x) â‰« (chainsMap f Ï†).f n =
      Ï†.hom â‰« ModuleCat.ofHom (lsingle (f âˆ˜ x)) := by
  ext
  simp [chainsMap_f]

lemma chainsMap_f_single (n : â„•) (x : Fin n â†’ G) (a : A) :
    (chainsMap f Ï†).f n (single x a) = single (f âˆ˜ x) (Ï†.hom a) := by
  simp [chainsMap_f]

@[simp]
lemma chainsMap_id :
    chainsMap (MonoidHom.id G) (ğŸ™ A) = ğŸ™ (inhomogeneousChains A) :=
  HomologicalComplex.hom_ext _ _ fun _ => ModuleCat.hom_ext <| lhom_ext' fun _ =>
    ModuleCat.hom_ext_iff.1 <| lsingle_comp_chainsMap_f (k := k) (MonoidHom.id G) ..

@[simp]
lemma chainsMap_id_f_eq_mapRange {A B : Rep k G} (i : â„•) (Ï† : A âŸ¶ B) :
    (chainsMap (MonoidHom.id G) Ï†).f i = ModuleCat.ofHom (mapRange.linearMap Ï†.hom.hom) := by
  refine ModuleCat.hom_ext <| lhom_ext fun _ _ => ?_
  simp [chainsMap_f, MonoidHom.coe_id]

lemma chainsMap_comp {G H K : Type u} [Group G] [Group H] [Group K]
    [DecidableEq G] [DecidableEq H] [DecidableEq K] {A : Rep k G} {B : Rep k H} {C : Rep k K}
    (f : G â†’* H) (g : H â†’* K) (Ï† : A âŸ¶ (Action.res _ f).obj B) (Ïˆ : B âŸ¶ (Action.res _ g).obj C) :
    chainsMap (g.comp f) (Ï† â‰« (Action.res _ f).map Ïˆ) = chainsMap f Ï† â‰« chainsMap g Ïˆ := by
  ext : 1
  refine ModuleCat.hom_ext <| lhom_ext fun _ _ => ?_
  simp [chainsMap_f, Function.comp_assoc]

lemma chainsMap_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) :
    chainsMap (MonoidHom.id G) (Ï† â‰« Ïˆ) =
      chainsMap (MonoidHom.id G) Ï† â‰« chainsMap (MonoidHom.id G) Ïˆ :=
  chainsMap_comp (MonoidHom.id G) (MonoidHom.id G) _ _

@[simp]
lemma chainsMap_zero : chainsMap f (0 : A âŸ¶ (Action.res _ f).obj B) = 0 :=
  HomologicalComplex.hom_ext _ _ <| fun _ => ModuleCat.hom_ext <| lhom_ext' <|
    fun _ => LinearMap.ext fun _ => by simp [chainsMap_f, LinearMap.zero_apply (Mâ‚‚ := B)]

lemma chainsMap_f_map_mono (hf : Function.Injective f) [Mono Ï†] (i : â„•) :
    Mono ((chainsMap f Ï†).f i) := by
  simpa [ModuleCat.mono_iff_injective] using
    (mapRange_injective Ï†.hom (map_zero _) <| (Rep.mono_iff_injective Ï†).1
    inferInstance).comp (mapDomain_injective hf.comp_left)

instance chainsMap_id_f_map_mono {A B : Rep k G} (Ï† : A âŸ¶ B) [Mono Ï†] (i : â„•) :
    Mono ((chainsMap (MonoidHom.id G) Ï†).f i) :=
  chainsMap_f_map_mono (MonoidHom.id G) Ï† (fun _ _ h => h) _

lemma chainsMap_f_map_epi (hf : Function.Surjective f) [Epi Ï†] (i : â„•) :
    Epi ((chainsMap f Ï†).f i) := by
  simpa [ModuleCat.epi_iff_surjective] using
    (mapRange_surjective Ï†.hom (map_zero _) ((Rep.epi_iff_surjective Ï†).1 inferInstance)).comp
    (mapDomain_surjective hf.comp_left)

instance chainsMap_id_f_map_epi {A B : Rep k G} (Ï† : A âŸ¶ B) [Epi Ï†] (i : â„•) :
    Epi ((chainsMap (MonoidHom.id G) Ï†).f i) :=
  chainsMap_f_map_epi _ _ (fun x => âŸ¨x, rflâŸ©) _

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map `Zâ‚™(G, A) âŸ¶ Zâ‚™(H, B)` sending `âˆ‘ aáµ¢Â·gáµ¢ : Gâ¿ â†’â‚€ A` to
`âˆ‘ Ï†(aáµ¢)Â·(f âˆ˜ gáµ¢) : Hâ¿ â†’â‚€ B`. -/
noncomputable abbrev cyclesMap (n : â„•) :
    groupHomology.cycles A n âŸ¶ groupHomology.cycles B n :=
  HomologicalComplex.cyclesMap (chainsMap f Ï†) n

theorem cyclesMap_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) (n : â„•) :
    cyclesMap (MonoidHom.id G) (Ï† â‰« Ïˆ) n =
      cyclesMap (MonoidHom.id G) Ï† n â‰« cyclesMap (MonoidHom.id G) Ïˆ n := by
  simp [cyclesMap, chainsMap_id_comp, HomologicalComplex.cyclesMap_comp]

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map `Hâ‚™(G, A) âŸ¶ Hâ‚™(H, B)` sending `âˆ‘ aáµ¢Â·gáµ¢ : Gâ¿ â†’â‚€ A` to
`âˆ‘ Ï†(aáµ¢)Â·(f âˆ˜ gáµ¢) : Hâ¿ â†’â‚€ B`. -/
noncomputable abbrev map (n : â„•) :
    groupHomology A n âŸ¶ groupHomology B n :=
  HomologicalComplex.homologyMap (chainsMap f Ï†) n

theorem map_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) (n : â„•) :
    map (MonoidHom.id G) (Ï† â‰« Ïˆ) n =
      map (MonoidHom.id G) Ï† n â‰« map (MonoidHom.id G) Ïˆ n := by
  rw [map, chainsMap_id_comp, HomologicalComplex.homologyMap_comp]

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map sending `âˆ‘ aáµ¢Â·gáµ¢ : G â†’â‚€ A` to `âˆ‘ Ï†(aáµ¢)Â·f(gáµ¢) : H â†’â‚€ B` -/
noncomputable abbrev fOne : (G â†’â‚€ A) â†’â‚—[k] H â†’â‚€ B :=
  mapRange.linearMap Ï†.hom.hom âˆ˜â‚— lmapDomain A k f

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map sending `âˆ‘ aáµ¢Â·(gáµ¢â‚, gáµ¢â‚‚) : G Ã— G â†’â‚€ A` to
`âˆ‘ Ï†(aáµ¢)Â·(f(gáµ¢â‚), f(gáµ¢â‚‚)) : H Ã— H â†’â‚€ B`.  -/
noncomputable abbrev fTwo : (G Ã— G â†’â‚€ A) â†’â‚—[k] H Ã— H â†’â‚€ B :=
  mapRange.linearMap Ï†.hom.hom âˆ˜â‚— lmapDomain A k (Prod.map f f)

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map sending `âˆ‘ aáµ¢Â·(gáµ¢â‚, gáµ¢â‚‚, gáµ¢â‚ƒ) : G Ã— G Ã— G â†’â‚€ A` to
`âˆ‘ Ï†(aáµ¢)Â·(f(gáµ¢â‚), f(gáµ¢â‚‚), f(gáµ¢â‚ƒ)) : H Ã— H Ã— H â†’â‚€ B`.  -/
noncomputable abbrev fThree : (G Ã— G Ã— G â†’â‚€ A) â†’â‚—[k] H Ã— H Ã— H â†’â‚€ B :=
  mapRange.linearMap Ï†.hom.hom âˆ˜â‚— lmapDomain A k (Prod.map f (Prod.map f f))

@[reassoc]
lemma chainsMap_f_0_comp_zeroChainsLequiv :
    (chainsMap f Ï†).f 0 â‰« (zeroChainsLequiv B).toModuleIso.hom =
      (zeroChainsLequiv A).toModuleIso.hom â‰« Ï†.hom := by
  refine ModuleCat.hom_ext <| lhom_ext' fun x => ModuleCat.homEquiv.symm.bijective.1 ?_
  ext y
  simp [ModuleCat.homEquiv, zeroChainsLequiv, Unique.eq_default]

@[reassoc]
lemma chainsMap_f_1_comp_oneChainsLequiv :
    (chainsMap f Ï†).f 1 â‰« (oneChainsLequiv B).toModuleIso.hom =
      (oneChainsLequiv A).toModuleIso.hom â‰« ModuleCat.ofHom (fOne f Ï†) := by
  refine ModuleCat.hom_ext <| lhom_ext' fun x => ModuleCat.homEquiv.symm.bijective.1 ?_
  ext y
  simp [ModuleCat.homEquiv, oneChainsLequiv, fOne]

@[reassoc]
lemma chainsMap_f_2_comp_twoChainsLequiv :
    (chainsMap f Ï†).f 2 â‰« (twoChainsLequiv B).toModuleIso.hom =
      (twoChainsLequiv A).toModuleIso.hom â‰« ModuleCat.ofHom (fTwo f Ï†) := by
  refine ModuleCat.hom_ext <| lhom_ext' fun x => ModuleCat.homEquiv.symm.bijective.1 ?_
  ext y
  simp [ModuleCat.homEquiv, twoChainsLequiv, fTwo]

@[reassoc]
lemma chainsMap_f_3_comp_threeChainsLequiv :
    (chainsMap f Ï†).f 3 â‰« (threeChainsLequiv B).toModuleIso.hom =
      (threeChainsLequiv A).toModuleIso.hom â‰« ModuleCat.ofHom (fThree f Ï†) := by
  refine ModuleCat.hom_ext <| lhom_ext' fun x => ModuleCat.homEquiv.symm.bijective.1 ?_
  ext y
  simp [ModuleCat.homEquiv, threeChainsLequiv, fThree, â† Fin.comp_tail]

open ShortComplex

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map `A_G âŸ¶ B_H`. -/
noncomputable def H0Map : H0 A âŸ¶ H0 B :=
  ModuleCat.ofHom <| Submodule.mapQ _ _ Ï†.hom.hom <| Submodule.span_le.2 <| fun _ âŸ¨âŸ¨g, yâŸ©, hyâŸ© =>
    mem_augmentationSubmodule_of_eq (f g) (Ï†.hom y) _ <| by
      simpa [â† hy] using (hom_comm_apply Ï† _ _).symm

omit [DecidableEq G] in
@[simp]
theorem H0Map_id : H0Map (MonoidHom.id G) (ğŸ™ A) = ğŸ™ _ :=
  ModuleCat.hom_ext <| Submodule.linearMap_qext _ rfl

theorem H0Map_comp {G H K : Type u} [Group G] [Group H] [Group K]
    {A : Rep k G} {B : Rep k H} {C : Rep k K} (f : G â†’* H) (g : H â†’* K)
    (Ï† : A âŸ¶ (Action.res _ f).obj B) (Ïˆ : B âŸ¶ (Action.res _ g).obj C) :
    H0Map (g.comp f) (Ï† â‰« (Action.res _ f).map Ïˆ) = H0Map f Ï† â‰« H0Map g Ïˆ :=
  ModuleCat.hom_ext <| Submodule.linearMap_qext _ rfl

omit [DecidableEq G] in
theorem H0Map_eq_coinvariantsFunctor_map {A B : Rep k G} (f : A âŸ¶ B) :
    H0Map (MonoidHom.id G) f = (coinvariantsFunctor k G).map f := by
  rfl

instance epi_H0Map_of_epi {A B : Rep k G} (f : A âŸ¶ B) [Epi f] :
    Epi (H0Map (MonoidHom.id G) f) :=
  (inferInstanceAs (Epi <| (coinvariantsFunctor k G).map f))

omit [DecidableEq G] [DecidableEq H] in
@[reassoc (attr := simp), elementwise (attr := simp)]
theorem H0Ï€_comp_H0Map :
    H0Ï€ A â‰« H0Map f Ï† = Ï†.hom â‰« H0Ï€ B := rfl

@[reassoc (attr := simp), elementwise (attr := simp)]
theorem map_comp_isoH0_hom :
    map f Ï† 0 â‰« (isoH0 B).hom = (isoH0 A).hom â‰« H0Map f Ï† := by
  simp [isoZeroCycles, â† cancel_epi (groupHomologyÏ€ _ _),
    chainsMap_f_0_comp_zeroChainsLequiv_assoc f Ï†, â† LinearEquiv.toModuleIso_hom]

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map from the short complex `(G Ã— G â†’â‚€ A) --dOne--> (G â†’â‚€ A) --dZero--> A`
to `(H Ã— H â†’â‚€ B) --dOne--> (H â†’â‚€ B) --dZero--> B`. -/
@[simps]
noncomputable def mapShortComplexH1 :
    shortComplexH1 A âŸ¶ shortComplexH1 B where
  Ï„â‚ := ModuleCat.ofHom (fTwo f Ï†)
  Ï„â‚‚ := ModuleCat.ofHom (fOne f Ï†)
  Ï„â‚ƒ := Ï†.hom
  commâ‚â‚‚ := ModuleCat.hom_ext <| lhom_ext fun a b => by
    simpa [dOne, map_add, map_sub, shortComplexH1, fTwo, fOne, â† map_inv]
      using congr(Finsupp.single (f a.2) $((hom_comm_apply Ï† _ _).symm))
  commâ‚‚â‚ƒ := ModuleCat.hom_ext <| lhom_ext fun a b => by
    simpa [map_add, map_sub, shortComplexH1, fOne, â† map_inv]
      using (hom_comm_apply Ï† _ _).symm

@[simp]
theorem mapShortComplexH1_zero :
    mapShortComplexH1 (A := A) (B := B) f 0 = 0 := by
  refine ShortComplex.hom_ext _ _ ?_ ?_ rfl
  all_goals
  { refine ModuleCat.hom_ext <| lhom_ext fun _ _ => ?_
    show mapRange.linearMap 0 (mapDomain _ (single _ _)) = 0
    simp [LinearMap.zero_apply (Mâ‚‚ := B)] }

@[simp]
theorem mapShortComplexH1_id : mapShortComplexH1 (MonoidHom.id G) (ğŸ™ A) = ğŸ™ _ := by
  refine ShortComplex.hom_ext _ _ ?_ ?_ rfl
  all_goals
  { refine ModuleCat.hom_ext <| Finsupp.lhom_ext fun _ _ => ?_
    show Finsupp.mapRange.linearMap LinearMap.id _ = Finsupp.single _ _
    simp [MonoidHom.coe_id] }

theorem mapShortComplexH1_comp {G H K : Type u} [Group G] [Group H] [Group K]
    [DecidableEq G] [DecidableEq H] [DecidableEq K]
    {A : Rep k G} {B : Rep k H} {C : Rep k K} (f : G â†’* H) (g : H â†’* K)
    (Ï† : A âŸ¶ (Action.res _ f).obj B) (Ïˆ : B âŸ¶ (Action.res _ g).obj C) :
    mapShortComplexH1 (g.comp f) (Ï† â‰« (Action.res _ f).map Ïˆ) =
      (mapShortComplexH1 f Ï†) â‰« (mapShortComplexH1 g Ïˆ) := by
  refine ShortComplex.hom_ext _ _ ?_ ?_ rfl
  all_goals
  { refine ModuleCat.hom_ext <| lhom_ext fun _ _ => ?_
    simp [shortComplexH1, Prod.map, fTwo, fOne] }

theorem mapShortComplexH1_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) :
    mapShortComplexH1 (MonoidHom.id G) (Ï† â‰« Ïˆ) =
      mapShortComplexH1 (MonoidHom.id G) Ï† â‰« mapShortComplexH1 (MonoidHom.id G) Ïˆ :=
  mapShortComplexH1_comp (MonoidHom.id G) (MonoidHom.id G) _ _

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map `Zâ‚(G, A) âŸ¶ Zâ‚(H, B)`. -/
noncomputable abbrev mapOneCycles :
    ModuleCat.of k (oneCycles A) âŸ¶ ModuleCat.of k (oneCycles B) :=
  ShortComplex.cyclesMap' (mapShortComplexH1 f Ï†) (shortComplexH1 A).moduleCatLeftHomologyData
    (shortComplexH1 B).moduleCatLeftHomologyData

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma mapOneCycles_comp_subtype :
    mapOneCycles f Ï† â‰« ModuleCat.ofHom (oneCycles B).subtype =
      ModuleCat.ofHom (fOne f Ï† âˆ˜â‚— (oneCycles A).subtype) :=
  ShortComplex.cyclesMap'_i (mapShortComplexH1 f Ï†) (moduleCatLeftHomologyData _)
    (moduleCatLeftHomologyData _)

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma cyclesMap_comp_isoOneCycles_hom :
    cyclesMap f Ï† 1 â‰« (isoOneCycles B).hom =
      (isoOneCycles A).hom â‰« mapOneCycles f Ï† := by
  simp_rw [â† cancel_mono (moduleCatLeftHomologyData (shortComplexH1 B)).i, mapOneCycles,
    Category.assoc, cyclesMap'_i, isoOneCycles, â† Category.assoc]
  simp [chainsMap_f_1_comp_oneChainsLequiv f Ï†, mapShortComplexH1, â† LinearEquiv.toModuleIso_hom]

variable (A) in
instance mapOneCycles_quotientGroupMk'_epi (S : Subgroup G) [S.Normal]
    [DecidableEq (G â§¸ S)] [IsTrivial (A.Ï.comp S.subtype)] :
    Epi (mapOneCycles (QuotientGroup.mk' S) (resOfQuotientIso A S).inv) := by
  rw [ModuleCat.epi_iff_surjective]
  rintro âŸ¨x, hxâŸ©
  choose! s hs using QuotientGroup.mk_surjective (s := S)
  have hsâ‚ : QuotientGroup.mk âˆ˜ s = id := funext hs
  refine âŸ¨âŸ¨mapDomain s x, ?_âŸ©, Subtype.ext <| by
    rw [mapOneCycles_comp_subtype_apply]; simp [â† mapDomain_comp, hsâ‚]âŸ©
  simpa [mem_oneCycles_iff, â† (mem_oneCycles_iff _).1 hx, sum_mapDomain_index_inj (f := s)
      (fun x y h => by rw [â† hs x, â† hs y, h])]
    using Finsupp.sum_congr fun a b => QuotientGroup.induction_on a fun a => by
      simp [â† QuotientGroup.mk_inv, Ï_eq_of_coe_eq A.Ï S (s a)â»Â¹ aâ»Â¹ (by simp [hs])]

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map `Hâ‚(G, A) âŸ¶ Hâ‚(H, B)`. -/
noncomputable abbrev H1Map : H1 A âŸ¶ H1 B :=
  ShortComplex.leftHomologyMap' (mapShortComplexH1 f Ï†)
    (shortComplexH1 A).moduleCatLeftHomologyData (shortComplexH1 B).moduleCatLeftHomologyData

@[simp]
theorem H1Map_id : H1Map (MonoidHom.id G) (ğŸ™ A) = ğŸ™ _ := by
  simp only [H1Map, shortComplexH1, mapShortComplexH1_id, leftHomologyMap'_id]
  rfl

theorem H1Map_comp {G H K : Type u} [Group G] [Group H] [Group K]
    [DecidableEq G] [DecidableEq H] [DecidableEq K]
    {A : Rep k G} {B : Rep k H} {C : Rep k K} (f : G â†’* H) (g : H â†’* K)
    (Ï† : A âŸ¶ (Action.res _ f).obj B) (Ïˆ : B âŸ¶ (Action.res _ g).obj C) :
    H1Map (g.comp f) (Ï† â‰« (Action.res _ f).map Ïˆ) = H1Map f Ï† â‰« H1Map g Ïˆ := by
  simpa [H1Map, shortComplexH1, mapShortComplexH1_comp] using leftHomologyMap'_comp _ _ _ _ _

theorem H1Map_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) :
    H1Map (MonoidHom.id G) (Ï† â‰« Ïˆ) = H1Map (MonoidHom.id G) Ï† â‰« H1Map (MonoidHom.id G) Ïˆ :=
  H1Map_comp (MonoidHom.id G) (MonoidHom.id G) _ _

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma H1Ï€_comp_H1Map :
    H1Ï€ A â‰« H1Map f Ï† = mapOneCycles f Ï† â‰« H1Ï€ B :=
  leftHomologyÏ€_naturality' (mapShortComplexH1 f Ï†) _ _

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma map_comp_isoH1_hom :
    map f Ï† 1 â‰« (isoH1 B).hom = (isoH1 A).hom â‰« H1Map f Ï† := by
  simp [â† cancel_epi (groupHomologyÏ€ _ _), H1Map, Category.assoc, (leftHomologyÏ€_naturality'
    (mapShortComplexH1 f Ï†) (moduleCatLeftHomologyData _) (moduleCatLeftHomologyData _)).symm]

@[simp]
lemma H1Map_one (Ï† : A âŸ¶ (Action.res _ (1 : G â†’* H)).obj B) :
    H1Map (1 : G â†’* H) Ï† = 0 := by
  simp only [â† cancel_epi (H1Ï€ A), H1Ï€_comp_H1Map, Limits.comp_zero]
  ext x
  rw [ModuleCat.hom_comp]
  refine (H1Ï€_eq_zero_iff _).2 ?_
  simpa [â† mapDomain_mapRange] using
    Submodule.finsupp_sum_mem _ _ _ _ fun _ _ => single_one_mem_oneBoundaries _

section CoresCoinf

variable (A) (S : Subgroup G) [S.Normal] [DecidableEq (G â§¸ S)]

section OfTrivial

variable [IsTrivial (A.Ï.comp S.subtype)]

/-- Given a `G`-representation `A` on which a normal subgroup `S â‰¤ G` acts trivially, this is the
short complex `Hâ‚(S, A) âŸ¶ Hâ‚(G, A) âŸ¶ Hâ‚(G â§¸ S, A)`. -/
@[simps Xâ‚ Xâ‚‚ Xâ‚ƒ f g]
noncomputable def H1CoresCoinfOfTrivial :
    ShortComplex (ModuleCat k) where
  Xâ‚ := H1 ((Action.res _ S.subtype).obj A)
  Xâ‚‚ := H1 A
  Xâ‚ƒ := H1 (ofQuotient A S)
  f := H1Map S.subtype (ğŸ™ _)
  g := H1Map (QuotientGroup.mk' S) <| (resOfQuotientIso A S).inv
  zero := by
    rw [â† H1Map_comp, congr (QuotientGroup.mk'_comp_subtype S) H1Map, H1Map_one]

instance H1Map_quotientGroupMk'_epi :
    Epi (H1Map (QuotientGroup.mk' S) (resOfQuotientIso A S).inv) := by
  convert epi_of_epi (H1Ï€ A) _
  rw [H1Ï€_comp_H1Map]
  exact @epi_comp _ _ _ _ _ _ (mapOneCycles_quotientGroupMk'_epi A S) (H1Ï€ _) inferInstance

/-- Given a `G`-representation `A` on which a normal subgroup `S â‰¤ G` acts trivially, the
induced map `Hâ‚(G, A) âŸ¶ Hâ‚(G â§¸ S, A)` is an epimorphism. -/
instance H1CoresCoinfOfTrivial_g_epi :
    Epi (H1CoresCoinfOfTrivial A S).g :=
  inferInstanceAs <| Epi (H1Map _ _)

/-- Given a `G`-representation `A` on which a normal subgroup `S â‰¤ G` acts trivially, the short
complex `Hâ‚(S, A) âŸ¶ Hâ‚(G, A) âŸ¶ Hâ‚(G â§¸ S, A)` is exact. -/
theorem H1CoresCoinfOfTrivial_exact :
    (H1CoresCoinfOfTrivial A S).Exact := by
  rw [ShortComplex.moduleCat_exact_iff_ker_sub_range]
  intro x hx
/- Denote `C(i) : C(S, A) âŸ¶ C(G, A), C(Ï€) : C(G, A) âŸ¶ C(G â§¸ S, A)` and let `x : Zâ‚(G, A)` map to
0 in `Hâ‚(G â§¸ S, A)`. -/
  induction' x using H1_induction_on with x
  rcases x with âŸ¨x, hxcâŸ©
  simp_all only [H1CoresCoinfOfTrivial_Xâ‚‚, H1CoresCoinfOfTrivial_Xâ‚ƒ, H1CoresCoinfOfTrivial_g,
    LinearMap.mem_ker, H1Ï€_comp_H1Map_apply (QuotientGroup.mk' S)]
/- Choose `y := âˆ‘ y(Ïƒ, Ï„)Â·(Ïƒ, Ï„) âˆˆ Câ‚‚(G â§¸ S, A)` such that `Câ‚(Ï€)(x) = d(y)`. -/
  rcases (H1Ï€_eq_zero_iff _).1 hx with âŸ¨y, hyâŸ©
/- Let `s : G â§¸ S â†’ G` be a section of the quotient map. -/
  choose! s hs using QuotientGroup.mk'_surjective S
  have hsâ‚ : QuotientGroup.mk (s := S) âˆ˜ s = id := funext hs
/- Let `z := âˆ‘ y(Ïƒ, Ï„)Â·(s(Ïƒ), s(Ï„))`. -/
  let z : G Ã— G â†’â‚€ A := lmapDomain _ k (Prod.map s s) y
/- We have that `Câ‚‚(Ï€)(z) = y`. -/
  have hz : lmapDomain _ k (QuotientGroup.mk' S) (dOne A z) = dOne (A.ofQuotient S) y := by
    have := congr($((mapShortComplexH1 (QuotientGroup.mk' S)
      (resOfQuotientIso A S).inv).commâ‚â‚‚.symm) z)
    simp_all [shortComplexH1, z, â† mapDomain_comp, Prod.map_comp_map]
  let v := x - dOne _ z
/- We have `Câ‚(s âˆ˜ Ï€)(v) = âˆ‘ v(g)Â·s(Ï€(g)) = 0`, since `Câ‚(Ï€)(v) = dCâ‚(Ï€)(z) - Câ‚(Ï€)(dz) = 0` by
previous assumptions. -/
  have hv : mapDomain (s âˆ˜ QuotientGroup.mk) v = 0 := by
    rw [mapDomain_comp]
    simp_all [v, mapDomain, sum_sub_index]
  let e : G â†’ G Ã— G := fun (g : G) => (s (g : G â§¸ S), (s (g : G â§¸ S))â»Â¹ * g)
  have he : e.Injective := fun x y hxy => by
    obtain âŸ¨(hâ‚ : s _ = s _), (hâ‚‚ : _ * _ = _ * _)âŸ© := Prod.ext_iff.1 hxy
    exact (mul_right_inj _).1 (hâ‚ â–¸ hâ‚‚)
/- Let `ve := âˆ‘ v(g)Â·(s(Ï€(g)), s(Ï€(g))â»Â¹g)`. -/
  let ve : G Ã— G â†’â‚€ A := mapDomain e v
  have hS : (v + dOne _ ve).support.toSet âŠ† S := by
  /- We have `d(ve) = âˆ‘ Ï(s(Ï€(g))â»Â¹)(v(g))Â·s(Ï€(g))â»Â¹g - âˆ‘ v(g)Â·g + âˆ‘ v(g)Â·s(Ï€(g))`.
    The second sum is `v`, so cancels: -/
    simp only [dOne, coe_lsum, he, ve, sum_mapDomain_index_inj, mul_inv_cancel_left,
      LinearMap.add_apply, LinearMap.sub_apply, LinearMap.coe_comp, Function.comp_apply,
      lsingle_apply, sum_add, sum_sub, sum_single, â† add_assoc, add_sub_cancel, e]
    intro w hw
    Â· obtain (hl | hr) := Finset.mem_union.1 (support_add hw)
    /- The first sum clearly has support in `S`: -/
      Â· obtain âŸ¨t, _, htâŸ© := Finset.mem_biUnion.1 (support_sum hl)
        apply support_single_subset at ht
        simp_all [â† QuotientGroup.eq, hs]
    /- The third sum is 0, by `hv`. -/
      Â· simp_all [mapDomain]
  /- Now `v + d(ve)` has support in `S` and agrees with `x` in `Hâ‚(G, A)`: -/
  use H1Ï€ _ âŸ¨comapDomain Subtype.val (v + dOne _ ve) <|
    Set.injOn_of_injective Subtype.val_injective, ?_âŸ©
  Â· simp only [H1CoresCoinfOfTrivial_Xâ‚, H1CoresCoinfOfTrivial_f, Action.res_obj_V,
      ModuleCat.hom_ofHom, Submodule.mkQ_apply, H1Ï€_comp_H1Map_apply]
    refine (H1Ï€_eq_iff _ _).2 ?_
  /- Indeed, `v + d(ve) - x = d(ve - z) âˆˆ Bâ‚(G, A)`, since `v := x - dz`. -/
    use ve - z
    have := mapOneCycles_comp_subtype_apply (B := A) S.subtype (ğŸ™ _)
    have := mapDomain_comapDomain (Î± := S) Subtype.val Subtype.val_injective
      (v + dOne A ve) (fun x hx => âŸ¨âŸ¨x, hS hxâŸ©, rflâŸ©)
    simp_all [-mapOneCycles_comp_subtype_apply, v, add_sub_assoc, sub_add_sub_cancel']
  /- And `v + d(ve) := x - dz + d(ve)` is a 1-cycle because `x` is. -/
  Â· have : v + dOne _ ve âˆˆ oneCycles A := Submodule.add_mem _
      (Submodule.sub_mem _ hxc <| dOne_apply_mem_oneCycles _) (dOne_apply_mem_oneCycles _)
    rw [mem_oneCycles_iff] at this âŠ¢
    rwa [â† sum_comapDomain, â† sum_comapDomain (g := fun _ a => a)] at this <;>
    exact âŸ¨Set.mapsTo_preimage _ _, Set.injOn_of_injective Subtype.val_injective,
      fun x hx => âŸ¨âŸ¨x, hS hxâŸ©, hx, rflâŸ©âŸ©

end OfTrivial

end CoresCoinf

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map from the short complex
`(G Ã— G Ã— G â†’â‚€ A) --dTwo--> (G Ã— G â†’â‚€ A) --dOne--> (G â†’â‚€ A)` to
`(H Ã— H Ã— H â†’â‚€ B) --dTwo--> (H Ã— H â†’â‚€ B) --dOne--> (H â†’â‚€ B)`. -/
@[simps]
noncomputable def mapShortComplexH2 :
    shortComplexH2 A âŸ¶ shortComplexH2 B where
  Ï„â‚ := ModuleCat.ofHom (fThree f Ï†)
  Ï„â‚‚ := ModuleCat.ofHom (fTwo f Ï†)
  Ï„â‚ƒ := ModuleCat.ofHom (fOne f Ï†)
  commâ‚â‚‚ := ModuleCat.hom_ext <| lhom_ext fun a b => by
    simpa [dTwo, shortComplexH2, map_add, map_sub, fThree, fTwo, â† map_inv]
      using congr(Finsupp.single _ $((hom_comm_apply Ï† _ _).symm))
  commâ‚‚â‚ƒ := ModuleCat.hom_ext <| lhom_ext fun a b => by
    simpa [dOne, shortComplexH2, map_add, map_sub, fTwo, fOne, â† map_inv]
      using congr(Finsupp.single _ $((hom_comm_apply Ï† _ _).symm))

@[simp]
theorem mapShortComplexH2_zero :
    mapShortComplexH2 (A := A) (B := B) f 0 = 0 := by
  refine ShortComplex.hom_ext _ _ ?_ ?_ ?_
  all_goals
  { refine ModuleCat.hom_ext <| Finsupp.lhom_ext fun _ _ => ?_
    show Finsupp.mapRange.linearMap 0 (Finsupp.mapDomain _ (Finsupp.single _ _)) = 0
    simp [LinearMap.zero_apply (Mâ‚‚ := B)] }

@[simp]
theorem mapShortComplexH2_id : mapShortComplexH2 (MonoidHom.id _) (ğŸ™ A) = ğŸ™ _ := by
  refine ShortComplex.hom_ext _ _ ?_ ?_ ?_
  all_goals
  { refine ModuleCat.hom_ext <| Finsupp.lhom_ext fun _ _ => ?_
    show Finsupp.mapRange.linearMap LinearMap.id _ = Finsupp.single _ _
    simp [MonoidHom.coe_id] }

theorem mapShortComplexH2_comp {G H K : Type u} [Group G] [Group H] [Group K]
    [DecidableEq G] [DecidableEq H] [DecidableEq K]
    {A : Rep k G} {B : Rep k H} {C : Rep k K} (f : G â†’* H) (g : H â†’* K)
    (Ï† : A âŸ¶ (Action.res _ f).obj B) (Ïˆ : B âŸ¶ (Action.res _ g).obj C) :
    mapShortComplexH2 (g.comp f) (Ï† â‰« (Action.res _ f).map Ïˆ) =
      (mapShortComplexH2 f Ï†) â‰« (mapShortComplexH2 g Ïˆ) := by
  refine ShortComplex.hom_ext _ _ ?_ ?_ ?_
  all_goals
  { refine ModuleCat.hom_ext <| Finsupp.lhom_ext fun _ _ => ?_
    simp [shortComplexH2, Prod.map, fThree, fTwo, fOne] }

theorem mapShortComplexH2_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) :
    mapShortComplexH2 (MonoidHom.id G) (Ï† â‰« Ïˆ) =
      mapShortComplexH2 (MonoidHom.id G) Ï† â‰« mapShortComplexH2 (MonoidHom.id G) Ïˆ :=
  mapShortComplexH2_comp (MonoidHom.id G) (MonoidHom.id G) _ _

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map `Zâ‚‚(G, A) âŸ¶ Zâ‚‚(H, B)`. -/
noncomputable abbrev mapTwoCycles :
    ModuleCat.of k (twoCycles A) âŸ¶ ModuleCat.of k (twoCycles B) :=
  ShortComplex.cyclesMap' (mapShortComplexH2 f Ï†) (shortComplexH2 A).moduleCatLeftHomologyData
    (shortComplexH2 B).moduleCatLeftHomologyData

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma mapTwoCycles_comp_subtype :
    mapTwoCycles f Ï† â‰« ModuleCat.ofHom (twoCycles B).subtype =
      ModuleCat.ofHom (fTwo f Ï† âˆ˜â‚— (twoCycles A).subtype) :=
  ShortComplex.cyclesMap'_i (mapShortComplexH2 f Ï†) (moduleCatLeftHomologyData _)
    (moduleCatLeftHomologyData _)

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma cyclesMap_comp_isoTwoCycles_hom :
    cyclesMap f Ï† 2 â‰« (isoTwoCycles B).hom =
      (isoTwoCycles A).hom â‰« mapTwoCycles f Ï† := by
  simp_rw [â† cancel_mono (moduleCatLeftHomologyData (shortComplexH2 B)).i, mapTwoCycles,
      Category.assoc, cyclesMap'_i, isoTwoCycles, â† Category.assoc]
  simp [chainsMap_f_2_comp_twoChainsLequiv f Ï†, mapShortComplexH2, â† LinearEquiv.toModuleIso_hom]

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map `Hâ‚‚(G, A) âŸ¶ Hâ‚‚(H, B)`. -/
noncomputable abbrev H2Map : H2 A âŸ¶ H2 B :=
  ShortComplex.leftHomologyMap' (mapShortComplexH2 f Ï†)
    (shortComplexH2 A).moduleCatLeftHomologyData (shortComplexH2 B).moduleCatLeftHomologyData

@[simp]
theorem H2Map_id : H2Map (MonoidHom.id G) (ğŸ™ A) = ğŸ™ _ := by
  simp only [H2Map, shortComplexH2, mapShortComplexH2_id, leftHomologyMap'_id]
  rfl

theorem H2Map_comp {G H K : Type u} [Group G] [Group H] [Group K]
    [DecidableEq G] [DecidableEq H] [DecidableEq K]
    {A : Rep k G} {B : Rep k H} {C : Rep k K} (f : G â†’* H) (g : H â†’* K)
    (Ï† : A âŸ¶ (Action.res _ f).obj B) (Ïˆ : B âŸ¶ (Action.res _ g).obj C) :
    H2Map (g.comp f) (Ï† â‰« (Action.res _ f).map Ïˆ) = H2Map f Ï† â‰« H2Map g Ïˆ := by
  simpa [H2Map, shortComplexH2, mapShortComplexH2_comp] using leftHomologyMap'_comp _ _ _ _ _

theorem H2Map_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) :
    H2Map (MonoidHom.id G) (Ï† â‰« Ïˆ) = H2Map (MonoidHom.id G) Ï† â‰« H2Map (MonoidHom.id G) Ïˆ :=
  H2Map_comp (MonoidHom.id G) (MonoidHom.id G) _ _

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma H2Ï€_comp_H2Map :
     H2Ï€ A â‰« H2Map f Ï† = mapTwoCycles f Ï† â‰« H2Ï€ B :=
  leftHomologyÏ€_naturality' (mapShortComplexH2 f Ï†) _ _

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma map_comp_isoH2_hom :
    map f Ï† 2 â‰« (isoH2 B).hom = (isoH2 A).hom â‰« H2Map f Ï† := by
  simp [â† cancel_epi (groupHomologyÏ€ _ _), H2Map, Category.assoc]

variable (k G) in
/-- The functor sending a representation to its complex of inhomogeneous chains. -/
@[simps]
noncomputable def chainsFunctor [DecidableEq G] :
    Rep k G â¥¤ ChainComplex (ModuleCat k) â„• where
  obj A := inhomogeneousChains A
  map f := chainsMap (MonoidHom.id _) f
  map_id _ := chainsMap_id
  map_comp Ï† Ïˆ := chainsMap_comp (MonoidHom.id G) (MonoidHom.id G) Ï† Ïˆ

instance : (chainsFunctor k G).PreservesZeroMorphisms where
  map_zero X Y := by
    ext i : 1
    refine ModuleCat.hom_ext <| Finsupp.lhom_ext fun _ _ => ?_
    simp

variable (k G) in
/-- The functor sending a `G`-representation `A` to `Hâ‚™(G, A)`. -/
@[simps]
noncomputable def functor (n : â„•) : Rep k G â¥¤ ModuleCat k where
  obj A := groupHomology A n
  map {A B} Ï† := map (MonoidHom.id _) Ï† n
  map_id A := by simp [map, groupHomology]
  map_comp f g := by
    simp only [â† HomologicalComplex.homologyMap_comp, â† chainsMap_comp]
    rfl

instance (n : â„•) : (functor k G n).PreservesZeroMorphisms where
  map_zero _ _ := by simp [map]

end groupHomology
