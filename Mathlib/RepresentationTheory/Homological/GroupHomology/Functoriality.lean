/-
Copyright (c) 2025 Amelia Livingston. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Amelia Livingston
-/
import Mathlib.RepresentationTheory.Homological.GroupHomology.LowDegree

/-!
# Functoriality of group homology

Given a commutative ring `k`, a group homomorphism `f : G â†’* H`, a `k`-linear `G`-representation
`A`, a `k`-linear `H`-representation `B`, and a representation morphism `A âŸ¶ Res(f)(B)`, we get
a chain map `inhomogeneousChains A âŸ¶ inhomogeneousChains B` and hence maps on homology
`Hâ‚™(G, A) âŸ¶ Hâ‚™(H, B)`.

We also provide extra API for these maps in degrees 0, 1, 2.

## Main definitions

* `groupHomology.chainsMap f Ï†` is the map `inhomogeneousChains A âŸ¶ inhomogeneousChains B`
  induced by a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`.
* `groupHomology.map f Ï† n` is the map `Hâ‚™(G, A) âŸ¶ Hâ‚™(H, B)` induced by a group homomorphism
  `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`.
* `groupHomology.H1CoresCoinf A S` is the short complex `Hâ‚(S, A) âŸ¶ Hâ‚(G, A) âŸ¶ Hâ‚(G â§¸ S, A_S)`
  for a normal subgroup `S â‰¤ G` and a `G`-representation `A`.

-/

universe v u

namespace groupHomology

open CategoryTheory Rep Finsupp Representation

variable {k G H : Type u} [CommRing k] [Group G] [Group H]
  {A : Rep k G} {B : Rep k H} (f : G â†’* H) (Ï† : A âŸ¶ (Action.res _ f).obj B) (n : â„•)

theorem congr {fâ‚ fâ‚‚ : G â†’* H} (h : fâ‚ = fâ‚‚) {Ï† : A âŸ¶ (Action.res _ fâ‚).obj B} {T : Type*}
    (F : (f : G â†’* H) â†’ (Ï† : A âŸ¶ (Action.res _ f).obj B) â†’ T) :
    F fâ‚ Ï† = F fâ‚‚ (h â–¸ Ï†) := by
  subst h
  rfl

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the chain map sending `âˆ‘ aáµ¢Â·gáµ¢ : Gâ¿ â†’â‚€ A` to `âˆ‘ Ï†(aáµ¢)Â·(f âˆ˜ gáµ¢) : Hâ¿ â†’â‚€ B`. -/
@[simps! -isSimp f f_hom]
noncomputable def chainsMap :
    inhomogeneousChains A âŸ¶ inhomogeneousChains B where
  f i := ModuleCat.ofHom <| mapRange.linearMap Ï†.hom.hom âˆ˜â‚— lmapDomain A k (f âˆ˜ Â·)
  comm' i j (hij : _ = _) := by
    subst hij
    ext
    simpa [Fin.comp_contractNth, map_add, inhomogeneousChains.d]
      using congr(single _ $((hom_comm_apply Ï† (_)â»Â¹ _).symm))

@[reassoc (attr := simp)]
lemma lsingle_comp_chainsMap_f (n : â„•) (x : Fin n â†’ G) :
    ModuleCat.ofHom (lsingle x) â‰« (chainsMap f Ï†).f n =
      Ï†.hom â‰« ModuleCat.ofHom (lsingle (f âˆ˜ x)) := by
  ext
  simp [chainsMap_f]

lemma chainsMap_f_single (n : â„•) (x : Fin n â†’ G) (a : A) :
    (chainsMap f Ï†).f n (single x a) = single (f âˆ˜ x) (Ï†.hom a) := by
  simp [chainsMap_f]

@[simp]
lemma chainsMap_id :
    chainsMap (MonoidHom.id G) (ğŸ™ A) = ğŸ™ (inhomogeneousChains A) :=
  HomologicalComplex.hom_ext _ _ fun _ => ModuleCat.hom_ext <| lhom_ext' fun _ =>
    ModuleCat.hom_ext_iff.1 <| lsingle_comp_chainsMap_f (k := k) (MonoidHom.id G) ..

@[simp]
lemma chainsMap_id_f_hom_eq_mapRange {A B : Rep k G} (i : â„•) (Ï† : A âŸ¶ B) :
    ((chainsMap (MonoidHom.id G) Ï†).f i).hom = mapRange.linearMap Ï†.hom.hom := by
  refine lhom_ext fun _ _ => ?_
  simp [chainsMap_f, MonoidHom.coe_id]

lemma chainsMap_comp {G H K : Type u} [Group G] [Group H] [Group K]
    {A : Rep k G} {B : Rep k H} {C : Rep k K}
    (f : G â†’* H) (g : H â†’* K) (Ï† : A âŸ¶ (Action.res _ f).obj B) (Ïˆ : B âŸ¶ (Action.res _ g).obj C) :
    chainsMap (g.comp f) (Ï† â‰« (Action.res _ f).map Ïˆ) = chainsMap f Ï† â‰« chainsMap g Ïˆ := by
  ext
  simp [chainsMap_f, Function.comp_assoc]

lemma chainsMap_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) :
    chainsMap (MonoidHom.id G) (Ï† â‰« Ïˆ) =
      chainsMap (MonoidHom.id G) Ï† â‰« chainsMap (MonoidHom.id G) Ïˆ :=
  chainsMap_comp (MonoidHom.id G) (MonoidHom.id G) _ _

@[simp]
lemma chainsMap_zero : chainsMap f (0 : A âŸ¶ (Action.res _ f).obj B) = 0 := by
  ext; simp [chainsMap_f, LinearMap.zero_apply (Mâ‚‚ := B)]

lemma chainsMap_f_map_mono (hf : Function.Injective f) [Mono Ï†] (i : â„•) :
    Mono ((chainsMap f Ï†).f i) := by
  simpa [ModuleCat.mono_iff_injective] using
    (mapRange_injective Ï†.hom (map_zero _) <| (Rep.mono_iff_injective Ï†).1
    inferInstance).comp (mapDomain_injective hf.comp_left)

instance chainsMap_id_f_map_mono {A B : Rep k G} (Ï† : A âŸ¶ B) [Mono Ï†] (i : â„•) :
    Mono ((chainsMap (MonoidHom.id G) Ï†).f i) :=
  chainsMap_f_map_mono (MonoidHom.id G) Ï† (fun _ _ h => h) _

lemma chainsMap_f_map_epi (hf : Function.Surjective f) [Epi Ï†] (i : â„•) :
    Epi ((chainsMap f Ï†).f i) := by
  simpa [ModuleCat.epi_iff_surjective] using
    (mapRange_surjective Ï†.hom (map_zero _) ((Rep.epi_iff_surjective Ï†).1 inferInstance)).comp
    (mapDomain_surjective hf.comp_left)

instance chainsMap_id_f_map_epi {A B : Rep k G} (Ï† : A âŸ¶ B) [Epi Ï†] (i : â„•) :
    Epi ((chainsMap (MonoidHom.id G) Ï†).f i) :=
  chainsMap_f_map_epi _ _ (fun x => âŸ¨x, rflâŸ©) _

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map `Zâ‚™(G, A) âŸ¶ Zâ‚™(H, B)` sending `âˆ‘ aáµ¢Â·gáµ¢ : Gâ¿ â†’â‚€ A` to
`âˆ‘ Ï†(aáµ¢)Â·(f âˆ˜ gáµ¢) : Hâ¿ â†’â‚€ B`. -/
noncomputable abbrev cyclesMap (n : â„•) :
    groupHomology.cycles A n âŸ¶ groupHomology.cycles B n :=
  HomologicalComplex.cyclesMap (chainsMap f Ï†) n

@[simp]
lemma cyclesMap_id : cyclesMap (MonoidHom.id G) (ğŸ™ A) n = ğŸ™ _ := by
  simp [cyclesMap]

@[reassoc]
lemma cyclesMap_comp {G H K : Type u} [Group G] [Group H] [Group K]
    {A : Rep k G} {B : Rep k H} {C : Rep k K} (f : G â†’* H) (g : H â†’* K)
    (Ï† : A âŸ¶ (Action.res _ f).obj B) (Ïˆ : B âŸ¶ (Action.res _ g).obj C) (n : â„•) :
    cyclesMap (g.comp f) (Ï† â‰« (Action.res _ f).map Ïˆ) n = cyclesMap f Ï† n â‰« cyclesMap g Ïˆ n := by
  simp [cyclesMap, â† HomologicalComplex.cyclesMap_comp, â† chainsMap_comp]

theorem cyclesMap_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) (n : â„•) :
    cyclesMap (MonoidHom.id G) (Ï† â‰« Ïˆ) n =
      cyclesMap (MonoidHom.id G) Ï† n â‰« cyclesMap (MonoidHom.id G) Ïˆ n := by
  simp [cyclesMap, chainsMap_id_comp, HomologicalComplex.cyclesMap_comp]

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map `Hâ‚™(G, A) âŸ¶ Hâ‚™(H, B)` sending `âˆ‘ aáµ¢Â·gáµ¢ : Gâ¿ â†’â‚€ A` to
`âˆ‘ Ï†(aáµ¢)Â·(f âˆ˜ gáµ¢) : Hâ¿ â†’â‚€ B`. -/
noncomputable abbrev map (n : â„•) :
    groupHomology A n âŸ¶ groupHomology B n :=
  HomologicalComplex.homologyMap (chainsMap f Ï†) n

@[reassoc, elementwise]
theorem Ï€_map (n : â„•) :
    Ï€ A n â‰« map f Ï† n = cyclesMap f Ï† n â‰« Ï€ B n := by
  simp [map, cyclesMap]

@[simp]
lemma map_id : map (MonoidHom.id G) (ğŸ™ A) n = ğŸ™ _ := by
  simp [map, groupHomology]

@[reassoc]
lemma map_comp {G H K : Type u} [Group G] [Group H] [Group K]
    {A : Rep k G} {B : Rep k H} {C : Rep k K} (f : G â†’* H) (g : H â†’* K)
    (Ï† : A âŸ¶ (Action.res _ f).obj B) (Ïˆ : B âŸ¶ (Action.res _ g).obj C) (n : â„•) :
    map (g.comp f) (Ï† â‰« (Action.res _ f).map Ïˆ) n = map f Ï† n â‰« map g Ïˆ n := by
  simp [map, â† HomologicalComplex.homologyMap_comp, â† chainsMap_comp]

theorem map_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) (n : â„•) :
    map (MonoidHom.id G) (Ï† â‰« Ïˆ) n =
      map (MonoidHom.id G) Ï† n â‰« map (MonoidHom.id G) Ïˆ n := by
  rw [map, chainsMap_id_comp, HomologicalComplex.homologyMap_comp]

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map sending `âˆ‘ aáµ¢Â·gáµ¢ : G â†’â‚€ A` to `âˆ‘ Ï†(aáµ¢)Â·f(gáµ¢) : H â†’â‚€ B`. -/
noncomputable abbrev chainsMapâ‚ : ModuleCat.of k (G â†’â‚€ A) âŸ¶ ModuleCat.of k (H â†’â‚€ B) :=
  ModuleCat.ofHom <| mapRange.linearMap Ï†.hom.hom âˆ˜â‚— lmapDomain A k f

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map sending `âˆ‘ aáµ¢Â·(gáµ¢â‚, gáµ¢â‚‚) : G Ã— G â†’â‚€ A` to
`âˆ‘ Ï†(aáµ¢)Â·(f(gáµ¢â‚), f(gáµ¢â‚‚)) : H Ã— H â†’â‚€ B`. -/
noncomputable abbrev chainsMapâ‚‚ : ModuleCat.of k (G Ã— G â†’â‚€ A) âŸ¶ ModuleCat.of k (H Ã— H â†’â‚€ B) :=
  ModuleCat.ofHom <| mapRange.linearMap Ï†.hom.hom âˆ˜â‚— lmapDomain A k (Prod.map f f)

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map sending `âˆ‘ aáµ¢Â·(gáµ¢â‚, gáµ¢â‚‚, gáµ¢â‚ƒ) : G Ã— G Ã— G â†’â‚€ A` to
`âˆ‘ Ï†(aáµ¢)Â·(f(gáµ¢â‚), f(gáµ¢â‚‚), f(gáµ¢â‚ƒ)) : H Ã— H Ã— H â†’â‚€ B`. -/
noncomputable abbrev chainsMapâ‚ƒ :
    ModuleCat.of k (G Ã— G Ã— G â†’â‚€ A) âŸ¶ ModuleCat.of k (H Ã— H Ã— H â†’â‚€ B) :=
  ModuleCat.ofHom <| mapRange.linearMap Ï†.hom.hom âˆ˜â‚— lmapDomain A k (Prod.map f (Prod.map f f))

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma chainsMap_f_0_comp_chainsIsoâ‚€ :
    (chainsMap f Ï†).f 0 â‰« (chainsIsoâ‚€ B).hom = (chainsIsoâ‚€ A).hom â‰« Ï†.hom := by
  ext
  simp [chainsMap_f, Unique.eq_default (Î± := Fin 0 â†’ G), Unique.eq_default (Î± := Fin 0 â†’ H),
    chainsIsoâ‚€]

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma chainsMap_f_1_comp_chainsIsoâ‚ :
    (chainsMap f Ï†).f 1 â‰« (chainsIsoâ‚ B).hom = (chainsIsoâ‚ A).hom â‰« chainsMapâ‚ f Ï† := by
  ext x
  simp [chainsMap_f, chainsIsoâ‚]

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma chainsMap_f_2_comp_chainsIsoâ‚‚ :
    (chainsMap f Ï†).f 2 â‰« (chainsIsoâ‚‚ B).hom = (chainsIsoâ‚‚ A).hom â‰« chainsMapâ‚‚ f Ï† := by
  ext
  simp [chainsMap_f, chainsIsoâ‚‚]

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma chainsMap_f_3_comp_chainsIsoâ‚ƒ :
    (chainsMap f Ï†).f 3 â‰« (chainsIsoâ‚ƒ B).hom = (chainsIsoâ‚ƒ A).hom â‰« chainsMapâ‚ƒ f Ï† := by
  ext
  simp [chainsMap_f, chainsIsoâ‚ƒ, â† Fin.comp_tail]

open ShortComplex

section H0

@[reassoc (attr := simp), elementwise (attr := simp)]
theorem cyclesMap_comp_cyclesIsoâ‚€_hom :
    cyclesMap f Ï† 0 â‰« (cyclesIsoâ‚€ B).hom = (cyclesIsoâ‚€ A).hom â‰« Ï†.hom := by
  simp [cyclesIsoâ‚€]

@[reassoc (attr := simp), elementwise (attr := simp)]
theorem cyclesIsoâ‚€_inv_comp_cyclesMap :
    (cyclesIsoâ‚€ A).inv â‰« cyclesMap f Ï† 0 = Ï†.hom â‰« (cyclesIsoâ‚€ B).inv :=
  (CommSq.vert_inv âŸ¨cyclesMap_comp_cyclesIsoâ‚€_hom f Ï†âŸ©).w.symm

@[reassoc (attr := simp), elementwise (attr := simp)]
theorem H0Ï€_comp_map :
    H0Ï€ A â‰« map f Ï† 0 = Ï†.hom â‰« H0Ï€ B := by
  simp [H0Ï€]

@[reassoc (attr := simp), elementwise (attr := simp)]
theorem map_id_comp_H0Iso_hom {A B : Rep k G} (f : A âŸ¶ B) :
    map (MonoidHom.id G) f 0 â‰« (H0Iso B).hom =
      (H0Iso A).hom â‰« (coinvariantsFunctor k G).map f := by
  rw [â† cancel_epi (H0Ï€ A)]
  ext
  simp

instance epi_map_0_of_epi {A B : Rep k G} (f : A âŸ¶ B) [Epi f] :
    Epi (map (MonoidHom.id G) f 0) where
  left_cancellation g h hgh := by
    simp only [â† cancel_epi (H0Ï€ A)] at hgh
    simp_all [cancel_epi]

end H0

section H1

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map from the short complex `(G Ã— G â†’â‚€ A) --dâ‚‚â‚--> (G â†’â‚€ A) --dâ‚â‚€--> A`
to `(H Ã— H â†’â‚€ B) --dâ‚‚â‚--> (H â†’â‚€ B) --dâ‚â‚€--> B`. -/
@[simps]
noncomputable def mapShortComplexH1 :
    shortComplexH1 A âŸ¶ shortComplexH1 B where
  Ï„â‚ := chainsMapâ‚‚ f Ï†
  Ï„â‚‚ := chainsMapâ‚ f Ï†
  Ï„â‚ƒ := Ï†.hom
  commâ‚â‚‚ := by
    simp only [shortComplexH1]
    ext : 3
    simpa [dâ‚‚â‚, map_add, map_sub, â† map_inv] using congr(single _ $((hom_comm_apply Ï† _ _).symm))
  commâ‚‚â‚ƒ := by
    simp only [shortComplexH1]
    ext : 3
    simpa [â† map_inv, dâ‚â‚€] using (hom_comm_apply Ï† _ _).symm

@[simp]
theorem mapShortComplexH1_zero :
    mapShortComplexH1 (A := A) (B := B) f 0 = 0 := by
  refine ShortComplex.hom_ext _ _ ?_ ?_ rfl
  all_goals
  { simp only [shortComplexH1]
    ext
    simp }

@[simp]
theorem mapShortComplexH1_id : mapShortComplexH1 (MonoidHom.id G) (ğŸ™ A) = ğŸ™ _ := by
  simp only [shortComplexH1]
  ext <;> simp

theorem mapShortComplexH1_comp {G H K : Type u} [Group G] [Group H] [Group K]
    {A : Rep k G} {B : Rep k H} {C : Rep k K} (f : G â†’* H) (g : H â†’* K)
    (Ï† : A âŸ¶ (Action.res _ f).obj B) (Ïˆ : B âŸ¶ (Action.res _ g).obj C) :
    mapShortComplexH1 (g.comp f) (Ï† â‰« (Action.res _ f).map Ïˆ) =
      (mapShortComplexH1 f Ï†) â‰« (mapShortComplexH1 g Ïˆ) := by
  refine ShortComplex.hom_ext _ _ ?_ ?_ rfl
  all_goals
  { simp only [shortComplexH1]
    ext
    simp [Prod.map] }

theorem mapShortComplexH1_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) :
    mapShortComplexH1 (MonoidHom.id G) (Ï† â‰« Ïˆ) =
      mapShortComplexH1 (MonoidHom.id G) Ï† â‰« mapShortComplexH1 (MonoidHom.id G) Ïˆ :=
  mapShortComplexH1_comp (MonoidHom.id G) (MonoidHom.id G) _ _

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map `Zâ‚(G, A) âŸ¶ Zâ‚(H, B)`. -/
noncomputable abbrev mapCyclesâ‚ :
    ModuleCat.of k (cyclesâ‚ A) âŸ¶ ModuleCat.of k (cyclesâ‚ B) :=
  ShortComplex.cyclesMap' (mapShortComplexH1 f Ï†) (shortComplexH1 A).moduleCatLeftHomologyData
    (shortComplexH1 B).moduleCatLeftHomologyData

lemma mapCyclesâ‚_hom :
    (mapCyclesâ‚ f Ï†).hom = (chainsMapâ‚ f Ï†).hom.restrict (fun x _ => by
      have := congr($((mapShortComplexH1 f Ï†).commâ‚‚â‚ƒ) x); simp_all [cyclesâ‚, shortComplexH1]) :=
  rfl

@[reassoc, elementwise]
lemma mapCyclesâ‚_comp {G H K : Type u} [Group G] [Group H] [Group K]
    {A : Rep k G} {B : Rep k H} {C : Rep k K} (f : G â†’* H) (g : H â†’* K)
    (Ï† : A âŸ¶ (Action.res _ f).obj B) (Ïˆ : B âŸ¶ (Action.res _ g).obj C) :
    mapCyclesâ‚ (g.comp f) (Ï† â‰« (Action.res _ f).map Ïˆ) =
      mapCyclesâ‚ f Ï† â‰« mapCyclesâ‚ g Ïˆ := by
  rw [â† cyclesMap'_comp, â† mapShortComplexH1_comp]

@[reassoc, elementwise]
theorem mapCyclesâ‚_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) :
    mapCyclesâ‚ (MonoidHom.id G) (Ï† â‰« Ïˆ) =
      mapCyclesâ‚ (MonoidHom.id G) Ï† â‰« mapCyclesâ‚ (MonoidHom.id G) Ïˆ :=
  mapCyclesâ‚_comp (MonoidHom.id G) (MonoidHom.id G) _ _

@[reassoc, elementwise]
lemma mapCyclesâ‚_comp_i :
    mapCyclesâ‚ f Ï† â‰« (shortComplexH1 B).moduleCatLeftHomologyData.i =
      (shortComplexH1 A).moduleCatLeftHomologyData.i â‰« chainsMapâ‚ f Ï† := by
  simp

@[simp]
lemma coe_mapCyclesâ‚ (x) :
    (mapCyclesâ‚ f Ï† x).1 = chainsMapâ‚ f Ï† x := rfl

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma cyclesMap_comp_isoCyclesâ‚_hom :
    cyclesMap f Ï† 1 â‰« (isoCyclesâ‚ B).hom = (isoCyclesâ‚ A).hom â‰« mapCyclesâ‚ f Ï† := by
  simp [â† cancel_mono (moduleCatLeftHomologyData (shortComplexH1 B)).i, mapShortComplexH1,
    chainsMap_f_1_comp_chainsIsoâ‚ f]

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma H1Ï€_comp_map :
    H1Ï€ A â‰« map f Ï† 1 = mapCyclesâ‚ f Ï† â‰« H1Ï€ B := by
  simp [H1Ï€, Iso.inv_comp_eq, â† cyclesMap_comp_isoCyclesâ‚_hom_assoc]

@[simp]
lemma map_1_one (Ï† : A âŸ¶ (Action.res _ (1 : G â†’* H)).obj B) :
    map (1 : G â†’* H) Ï† 1 = 0 := by
  simp only [â† cancel_epi (H1Ï€ A), H1Ï€_comp_map, Limits.comp_zero]
  ext x
  rw [ModuleCat.hom_comp]
  refine (H1Ï€_eq_zero_iff _).2 ?_
  simpa [coe_mapCyclesâ‚ _ Ï† x, mapDomain, map_finsuppSum] using
    (boundariesâ‚ B).finsuppSum_mem k x.1 _ fun _ _ => single_one_mem_boundariesâ‚ (A := B) _

section CoresCoinf

variable (A) (S : Subgroup G) [S.Normal]

section OfTrivial

variable [IsTrivial (A.Ï.comp S.subtype)]

instance mapCyclesâ‚_quotientGroupMk'_epi :
    Epi (mapCyclesâ‚ (QuotientGroup.mk' S) (resOfQuotientIso A S).inv) := by
  rw [ModuleCat.epi_iff_surjective]
  rintro âŸ¨x, hxâŸ©
  choose! s hs using QuotientGroup.mk_surjective (s := S)
  have hsâ‚ : QuotientGroup.mk âˆ˜ s = id := funext hs
  refine âŸ¨âŸ¨mapDomain s x, ?_âŸ©, Subtype.ext <| by
    simp [mapCyclesâ‚_hom, â† mapDomain_comp, hsâ‚]âŸ©
  simpa [mem_cyclesâ‚_iff, â† (mem_cyclesâ‚_iff _).1 hx, sum_mapDomain_index_inj (f := s)
      (fun x y h => by rw [â† hs x, â† hs y, h])]
    using Finsupp.sum_congr fun a b => QuotientGroup.induction_on a fun a => by
      simp [â† QuotientGroup.mk_inv, apply_eq_of_coe_eq A.Ï S (s a)â»Â¹ aâ»Â¹ (by simp [hs])]

/-- Given a `G`-representation `A` on which a normal subgroup `S â‰¤ G` acts trivially, this is the
short complex `Hâ‚(S, A) âŸ¶ Hâ‚(G, A) âŸ¶ Hâ‚(G â§¸ S, A)`. -/
@[simps Xâ‚ Xâ‚‚ Xâ‚ƒ f g]
noncomputable def H1CoresCoinfOfTrivial :
    ShortComplex (ModuleCat k) where
  Xâ‚ := H1 ((Action.res _ S.subtype).obj A)
  Xâ‚‚ := H1 A
  Xâ‚ƒ := H1 (ofQuotient A S)
  f := map S.subtype (ğŸ™ _) 1
  g := map (QuotientGroup.mk' S) (resOfQuotientIso A S).inv 1
  zero := by rw [â† map_comp, congr (QuotientGroup.mk'_comp_subtype S) (map (n := 1)), map_1_one]

instance map_1_quotientGroupMk'_epi :
    Epi (map (QuotientGroup.mk' S) (resOfQuotientIso A S).inv 1) := by
  convert epi_of_epi (H1Ï€ A) _
  rw [H1Ï€_comp_map]
  exact @epi_comp _ _ _ _ _ _ (mapCyclesâ‚_quotientGroupMk'_epi A S) (H1Ï€ _) inferInstance

/-- Given a `G`-representation `A` on which a normal subgroup `S â‰¤ G` acts trivially, the
induced map `Hâ‚(G, A) âŸ¶ Hâ‚(G â§¸ S, A)` is an epimorphism. -/
instance H1CoresCoinfOfTrivial_g_epi :
    Epi (H1CoresCoinfOfTrivial A S).g :=
  inferInstanceAs <| Epi (map _ _ 1)

/-- Given a `G`-representation `A` on which a normal subgroup `S â‰¤ G` acts trivially, the short
complex `Hâ‚(S, A) âŸ¶ Hâ‚(G, A) âŸ¶ Hâ‚(G â§¸ S, A)` is exact. -/
theorem H1CoresCoinfOfTrivial_exact :
    (H1CoresCoinfOfTrivial A S).Exact := by
  classical
  rw [ShortComplex.moduleCat_exact_iff_ker_sub_range]
  intro x hx
/- Denote `C(i) : C(S, A) âŸ¶ C(G, A), C(Ï€) : C(G, A) âŸ¶ C(G â§¸ S, A)` and let `x : Zâ‚(G, A)` map to
0 in `Hâ‚(G â§¸ S, A)`. -/
  induction x using H1_induction_on with | @h x =>
  rcases x with âŸ¨x, hxcâŸ©
  simp_all only [H1CoresCoinfOfTrivial_Xâ‚‚, H1CoresCoinfOfTrivial_Xâ‚ƒ, H1CoresCoinfOfTrivial_g,
    LinearMap.mem_ker, H1Ï€_comp_map_apply (QuotientGroup.mk' S)]
/- Choose `y := âˆ‘ y(Ïƒ, Ï„)Â·(Ïƒ, Ï„) âˆˆ Câ‚‚(G â§¸ S, A)` such that `Câ‚(Ï€)(x) = d(y)`. -/
  rcases (H1Ï€_eq_zero_iff _).1 hx with âŸ¨y, hyâŸ©
/- Let `s : G â§¸ S â†’ G` be a section of the quotient map. -/
  choose! s hs using QuotientGroup.mk'_surjective S
  have hsâ‚ : QuotientGroup.mk (s := S) âˆ˜ s = id := funext hs
/- Let `z := âˆ‘ y(Ïƒ, Ï„)Â·(s(Ïƒ), s(Ï„))`. -/
  let z : G Ã— G â†’â‚€ A := lmapDomain _ k (Prod.map s s) y
/- We have that `Câ‚‚(Ï€)(z) = y`. -/
  have hz : lmapDomain _ k (QuotientGroup.mk' S) (dâ‚‚â‚ A z) = dâ‚‚â‚ (A.ofQuotient S) y := by
    have := congr($((mapShortComplexH1 (QuotientGroup.mk' S)
      (resOfQuotientIso A S).inv).commâ‚â‚‚.symm) z)
    simp_all [shortComplexH1, z, â† mapDomain_comp, Prod.map_comp_map]
  let v := x - dâ‚‚â‚ _ z
/- We have `Câ‚(s âˆ˜ Ï€)(v) = âˆ‘ v(g)Â·s(Ï€(g)) = 0`, since `Câ‚(Ï€)(v) = dCâ‚(Ï€)(z) - Câ‚(Ï€)(dz) = 0` by
previous assumptions. -/
  have hv : mapDomain (s âˆ˜ QuotientGroup.mk) v = 0 := by
    rw [mapDomain_comp]
    simp_all [v, mapDomain, sum_sub_index, coe_mapCyclesâ‚ _ _ âŸ¨x, hxcâŸ©]
  let e : G â†’ G Ã— G := fun (g : G) => (s (g : G â§¸ S), (s (g : G â§¸ S))â»Â¹ * g)
  have he : e.Injective := fun x y hxy => by
    obtain âŸ¨(hâ‚ : s _ = s _), (hâ‚‚ : _ * _ = _ * _)âŸ© := Prod.ext_iff.1 hxy
    exact (mul_right_inj _).1 (hâ‚ â–¸ hâ‚‚)
/- Let `ve := âˆ‘ v(g)Â·(s(Ï€(g)), s(Ï€(g))â»Â¹g)`. -/
  let ve : G Ã— G â†’â‚€ A := mapDomain e v
  have hS : (v + dâ‚‚â‚ _ ve).support.toSet âŠ† S := by
  /- We have `d(ve) = âˆ‘ Ï(s(Ï€(g))â»Â¹)(v(g))Â·s(Ï€(g))â»Â¹g - âˆ‘ v(g)Â·g + âˆ‘ v(g)Â·s(Ï€(g))`.
    The second sum is `v`, so cancels: -/
    simp only [dâ‚‚â‚, ve, ModuleCat.hom_ofHom, coe_lsum, sum_mapDomain_index_inj he, sum_single,
      LinearMap.add_apply, LinearMap.sub_apply, LinearMap.coe_comp, Function.comp_apply,
      lsingle_apply, sum_add, sum_sub, mul_inv_cancel_left, â† add_assoc, add_sub_cancel, e]
    intro w hw
    Â· obtain (hl | hr) := Finset.mem_union.1 (support_add hw)
    /- The first sum clearly has support in `S`: -/
      Â· obtain âŸ¨t, _, htâŸ© := Finset.mem_biUnion.1 (support_sum hl)
        apply support_single_subset at ht
        simp_all [â† QuotientGroup.eq]
    /- The third sum is 0, by `hv`. -/
      Â· simp_all [mapDomain]
  /- Now `v + d(ve)` has support in `S` and agrees with `x` in `Hâ‚(G, A)`: -/
  use H1Ï€ _ âŸ¨comapDomain Subtype.val (v + dâ‚‚â‚ _ ve) <|
    Set.injOn_of_injective Subtype.val_injective, ?_âŸ©
  Â· simp only [H1CoresCoinfOfTrivial_f, H1CoresCoinfOfTrivial_Xâ‚, H1Ï€_comp_map_apply]
    refine (H1Ï€_eq_iff _ _).2 ?_
  /- Indeed, `v + d(ve) - x = d(ve - z) âˆˆ Bâ‚(G, A)`, since `v := x - dz`. -/
    use ve - z
    have := mapDomain_comapDomain (Î± := S) Subtype.val Subtype.val_injective
      (v + dâ‚‚â‚ A ve) (fun x hx => âŸ¨âŸ¨x, hS hxâŸ©, rflâŸ©)
    simp_all [mapCyclesâ‚_hom, v, add_sub_assoc, sub_add_sub_cancel']
  /- And `v + d(ve) := x - dz + d(ve)` is a 1-cycle because `x` is. -/
  Â· have : v + dâ‚‚â‚ _ ve âˆˆ cyclesâ‚ A := Submodule.add_mem _
      (Submodule.sub_mem _ hxc <| dâ‚‚â‚_apply_mem_cyclesâ‚ _) (dâ‚‚â‚_apply_mem_cyclesâ‚ _)
    rw [mem_cyclesâ‚_iff] at this âŠ¢
    rwa [â† sum_comapDomain, â† sum_comapDomain (g := fun _ a => a)] at this <;>
    exact âŸ¨Set.mapsTo_preimage _ _, Set.injOn_of_injective Subtype.val_injective,
      fun x hx => âŸ¨âŸ¨x, hS hxâŸ©, hx, rflâŸ©âŸ©

end OfTrivial

/-- The short complex `Hâ‚(S, A) âŸ¶ Hâ‚(G, A) âŸ¶ Hâ‚(G â§¸ S, A_S)`. -/
@[simps Xâ‚ Xâ‚‚ Xâ‚ƒ f g]
noncomputable def H1CoresCoinf :
    ShortComplex (ModuleCat k) where
  Xâ‚ := H1 ((Action.res _ S.subtype).obj A)
  Xâ‚‚ := H1 A
  Xâ‚ƒ := H1 (quotientToCoinvariants A S)
  f := map S.subtype (ğŸ™ _) 1
  g := map (QuotientGroup.mk' S) (toCoinvariantsMkQ A S) 1
  zero := by rw [â† map_comp, congr (QuotientGroup.mk'_comp_subtype S) (map (n := 1)), map_1_one]

/-- Given a `G`-representation `A` and a normal subgroup `S â‰¤ G`, let `I(S)A` denote the submodule
of `A` spanned by elements of the form `Ï(s)(a) - a` for `s : S, a : A`. Then the image of
`Câ‚(G, I(S)A)` in `Câ‚(G, A)â§¸Bâ‚(G, A)` is contained in the image of `Câ‚(S, A)`. -/
theorem comap_coinvariantsKer_pOpcycles_range_subtype_pOpcycles_eq_top :
    Submodule.comap ((mapShortComplexH1 (MonoidHom.id G) (coinvariantsShortComplex A S).f).Ï„â‚‚ â‰«
      (shortComplexH1 _).pOpcycles).hom (LinearMap.range ((mapShortComplexH1 S.subtype (ğŸ™ _)).Ï„â‚‚ â‰«
      (shortComplexH1 _).pOpcycles).hom) = âŠ¤ := by
  rw [eq_top_iff]
  intro x _
  rcases mapRange_surjective _ (map_zero _) (chainsâ‚ToCoinvariantsKer_surjective
    ((Action.res _ S.subtype).obj A)) x with âŸ¨(X : G â†’â‚€ S â†’â‚€ A), hXâŸ©
  let Y : S â†’â‚€ A := X.sum fun g f =>
    mapRange.linearMap (A.Ï gâ»Â¹) (lmapDomain _ k (fun s => MulAut.conjNormal gâ»Â¹ s) f) - f
  let Z : G Ã— G â†’â‚€ A := X.sum fun g f =>
    lmapDomain _ k (fun s => (g, gâ»Â¹ * s.1 * g)) f - lmapDomain _ k (fun s => (s.1, g)) f
  use Y
  apply (moduleCat_pOpcycles_eq_iff _ _ _).2 âŸ¨Z, ?_âŸ©
  change dâ‚‚â‚ A Z = mapRange id rfl (lmapDomain _ k Subtype.val Y) -
    mapRange.linearMap (Submodule.subtype _) (mapDomain id x)
  simpa [map_finsuppSum, mapDomain, map_sub, â† hX, sum_single_index, finsuppProdLEquiv,
    finsuppProdEquiv, Finsupp.uncurry, dâ‚‚â‚, Y, Z, sum_mapRange_index,
    chainsâ‚ToCoinvariantsKer, dâ‚â‚€, single_sum, mul_assoc, sub_add_eq_add_sub,
    sum_sum_index, add_smul, sub_sub_sub_eq, lsingle, singleAddHom] using add_comm _ _

/-- Given a `G`-representation `A` and a normal subgroup `S â‰¤ G`, the map
`Hâ‚(G, A) âŸ¶ Hâ‚(G â§¸ S, A_S)` is an epimorphism. -/
instance [DecidableEq (G â§¸ S)] :
    Epi (H1CoresCoinf A S).g := by
  rw [ModuleCat.epi_iff_surjective]
  intro x
  induction x using H1_induction_on with | @h x =>
/- Let `x : Zâ‚(G â§¸ S, A_S)`. We know `Zâ‚(G, A_S) âŸ¶ Zâ‚(G â§¸ S, A_S)` is surjective, so pick
`y : Zâ‚(G, A_S)` in the preimage of `x`. -/
  rcases (ModuleCat.epi_iff_surjective _).1
    (mapCyclesâ‚_quotientGroupMk'_epi (A.toCoinvariants S) S) x with âŸ¨y, hyâŸ©
/- We know `Câ‚(G, A) âŸ¶ Câ‚(G, A_S)` is surjective, so pick `Y` in the preimage of `y`. -/
  rcases mapRange_surjective _ (map_zero _)
    (Coinvariants.mk_surjective (A.Ï.comp S.subtype)) y.1 with âŸ¨Y, hYâŸ©
/- Then `d(Y) âˆˆ I(S)A,` since `d(y) = 0`. -/
  have : dâ‚â‚€ _ Y âˆˆ Coinvariants.ker (A.Ï.comp S.subtype) := by
    have h' := congr($((mapShortComplexH1 (B := toCoinvariants A S)
      (MonoidHom.id G) (toCoinvariantsMkQ A S)).commâ‚‚â‚ƒ) Y)
    simp_all [shortComplexH1, â† Coinvariants.mk_eq_zero]
  /- Thus we can pick a representation of `d(Y)` as a sum `âˆ‘ Ï(sáµ¢â»Â¹)(aáµ¢) - aáµ¢`, `sáµ¢ âˆˆ S, aáµ¢ âˆˆ A`,
and `Y - âˆ‘ aáµ¢Â·sáµ¢` is a cycle. -/
  rcases chainsâ‚ToCoinvariantsKer_surjective
    ((Action.res _ S.subtype).obj A) âŸ¨dâ‚â‚€ A Y, thisâŸ© with âŸ¨(Z : S â†’â‚€ A), hZâŸ©
  have H : dâ‚â‚€ A (Y - mapDomain S.subtype Z) = 0 := by
    simpa [map_sub, sub_eq_zero, chainsâ‚ToCoinvariantsKer, - LinearMap.sub_apply, dâ‚â‚€,
      sum_mapDomain_index_inj] using Subtype.ext_iff.1 hZ.symm
  use H1Ï€ A âŸ¨Y - mapDomain S.subtype Z, HâŸ©
  simp only [H1CoresCoinf_Xâ‚ƒ, H1CoresCoinf_Xâ‚‚, H1CoresCoinf_g,
    Subgroup.coe_subtype, H1Ï€_comp_map_apply]
/- Moreover, the image of `Y - âˆ‘ aáµ¢Â·sáµ¢` in `Zâ‚(G â§¸ S, A_S)` is `x - âˆ‘ aáµ¢Â·1`, and hence differs from
`x` by a boundary, since `aáµ¢Â·1 = d(aáµ¢Â·(1, 1))`. -/
  refine (H1Ï€_eq_iff _ _).2 ?_
  simpa [â† hy, mapCyclesâ‚_hom, map_sub, mapRange_sub, hY, â† mapDomain_comp, â† mapDomain_mapRange,
    Function.comp_def, (QuotientGroup.eq_one_iff <| Subtype.val _).2 (Subtype.prop _)]
    using Submodule.finsuppSum_mem _ _ _ _ fun _ _ => single_one_mem_boundariesâ‚ _

/-- Given a `G`-representation `A` and a normal subgroup `S â‰¤ G`, the short complex
`Hâ‚(S, A) âŸ¶ Hâ‚(G, A) âŸ¶ Hâ‚(G â§¸ S, A_S)` is exact. `simp`s squeezed for performance. -/
theorem H1CoresCoinf_exact :
    (H1CoresCoinf A S).Exact := by
  rw [ShortComplex.moduleCat_exact_iff_ker_sub_range]
  intro x hx
  induction x using H1_induction_on with | @h x =>
  simp only [H1CoresCoinf_Xâ‚‚, H1CoresCoinf_Xâ‚ƒ, LinearMap.mem_ker, H1CoresCoinf_g,
    H1Ï€_comp_map_apply (QuotientGroup.mk' S)] at hx
/- Let `x : Zâ‚(G, A)` map to 0 in `Hâ‚(G, â§¸ S, A_S)`. Pick `y : Câ‚‚(G â§¸ S, A_S)` such that `d(y)`
equals `Zâ‚(Ï€, Ï€)(x) : Zâ‚(G â§¸ S, A_S)`. -/
  rcases (H1Ï€_eq_zero_iff _).1 hx with âŸ¨y, hyâŸ©
/- Then `Zâ‚(Ï€, Id)(x) : Zâ‚(G, A_S)` maps to 0 in `Hâ‚(G â§¸ S, A_S)`. We know
`Hâ‚(S, A_S) âŸ¶ Hâ‚(G, A_S) âŸ¶ Hâ‚(G â§¸ S, A_S)` is exact by `H1CoresCoinfOfTrivial_exact`, since
`S` acts trivially on `A_S`. So we can choose `z : Zâ‚(S, A_S)` with the same homology class as
`Zâ‚(Ï€, Id)(Ï€)` in `Hâ‚(G, A_S)`. -/
  rcases @(ShortComplex.moduleCat_exact_iff_ker_sub_range _).1
    (H1CoresCoinfOfTrivial_exact (toCoinvariants A S) S)
    (H1Ï€ _ <| mapCyclesâ‚ (MonoidHom.id G) (toCoinvariantsMkQ A S) x) (by
    simpa only [H1CoresCoinfOfTrivial_Xâ‚‚, H1CoresCoinfOfTrivial_Xâ‚ƒ, H1CoresCoinfOfTrivial_g,
      Iso.refl_inv, LinearMap.mem_ker, H1Ï€_comp_map_apply (QuotientGroup.mk' S),
      â† mapCyclesâ‚_comp_apply (x := x)] using hx) with âŸ¨z, hzâŸ©
  induction z using H1_induction_on with | @h z =>
  simp only [H1CoresCoinfOfTrivial_Xâ‚‚, H1CoresCoinfOfTrivial_Xâ‚, H1CoresCoinfOfTrivial_f] at hz
  rw [H1Ï€_comp_map_apply] at hz
/- Choose `w : Câ‚‚(G, A_S)` such that `d(w) = Zâ‚(i, Id)(z) - Zâ‚(Id, Ï€)(x)`. -/
  rcases (H1Ï€_eq_iff _ _).1 hz with âŸ¨w, hzwâŸ©
/- Choose `Z : Câ‚(S, A)` mapping to `z : Câ‚(S, A_S)`, and `W : Câ‚‚(G, A)` mapping to
`w : Câ‚‚(G, A_S)`. -/
  rcases mapRange_surjective (Coinvariants.mk _) (map_zero _)
    (Coinvariants.mk_surjective _) z.1 with âŸ¨Z, hZâŸ©
  rcases mapRange_surjective (Coinvariants.mk _) (map_zero _)
    (Coinvariants.mk_surjective _) w with âŸ¨W, hWâŸ©
/- Let `b : Câ‚(G, A)` denote `x + d(W) - Câ‚(i, Id)(z)`. -/
  let b : G â†’â‚€ A := (x.1 : G â†’â‚€ A) + dâ‚‚â‚ A W - lmapDomain _ k S.subtype Z
/- Then `b` has coefficients in `I(S)A := âŸ¨{Ï(s)(a) - a | s âˆˆ S, a âˆˆ A}âŸ©`, since
`Câ‚(G, I(S)(A)) âŸ¶ Câ‚(G, A) âŸ¶ Câ‚(G, A_S)` is exact, and `b` is in the kernel of the second map. -/
  have hb : âˆ€ g, b g âˆˆ Coinvariants.ker (A.Ï.comp S.subtype) :=
    fun g => (Coinvariants.mk_eq_iff _).1 <| by
      have := Finsupp.ext_iff.1 (congr($((mapShortComplexH1 (B := toCoinvariants A S)
        (MonoidHom.id G) (toCoinvariantsMkQ A S)).commâ‚â‚‚.symm) W)) g
      simp only [shortComplexH1, mapShortComplexH1_Ï„â‚‚, ModuleCat.ofHom_comp, MonoidHom.coe_id,
        lmapDomain_id, ModuleCat.ofHom_id, Action.res_obj_V, toCoinvariantsMkQ_hom,
        Category.id_comp, mapShortComplexH1_Ï„â‚, Prod.map_id, ModuleCat.hom_comp,
        ModuleCat.hom_ofHom, LinearMap.coe_comp, Function.comp_apply, mapRange.linearMap_apply,
        mapRange_apply, hW, hzw, mapCyclesâ‚_hom, Subgroup.coe_subtype, Action.id_hom,
        ModuleCat.hom_id, mapRange.linearMap_id, Category.comp_id, LinearMap.restrict_coe_apply,
        lmapDomain_apply, coe_sub, Pi.sub_apply, eq_sub_iff_add_eq'] at this
      simp only [â† mapRange_apply (f := Coinvariants.mk <| A.Ï.comp S.subtype)
        (hf := map_zero _) (a := g), â† mapRange.linearMap_apply (R := k)]
      simp only [mapRange.linearMap_apply, mapRange_apply, coe_add, Pi.add_apply, map_add, this,
        Subgroup.coe_subtype, lmapDomain_apply, implies_true, â† mapDomain_mapRange, hZ,
        Action.res_obj_V]
/- Let `Î²` be `b` considered as an element of `Câ‚(G, I(S)(A))`, so that `Câ‚(Id, i)(Î²) = b`. -/
  let Î² : G â†’â‚€ Coinvariants.ker (A.Ï.comp S.subtype) :=
    mapRange (Function.invFun <| (Coinvariants.ker (A.Ï.comp S.subtype)).subtype)
    (Function.leftInverse_invFun Subtype.val_injective (0 : Coinvariants.ker _)) b
  have hÎ²b : mapRange Subtype.val rfl Î² = b := Finsupp.ext fun g => Subtype.ext_iff.1 <|
    Function.leftInverse_invFun Subtype.val_injective âŸ¨b g, hb gâŸ©
/- Then, since the image of `Câ‚(G, I(S)A)` in `Câ‚(G, A)â§¸Bâ‚(G, A)` is contained in the image of
`Câ‚(S, A)` by `comap_coinvariantsKer_pOpcycles_range_subtype_pOpcycles_eq_top`, we can choose
`Î± : Câ‚(S, A)`, `Î´ : Câ‚‚(G, A)` such that `d(Î´) = Zâ‚(i, Id)(Î±) - Zâ‚(Id, i)(Î²)`. -/
  rcases eq_top_iff.1 (comap_coinvariantsKer_pOpcycles_range_subtype_pOpcycles_eq_top A S)
    (by trivial : Î² âˆˆ âŠ¤) with âŸ¨(Î± : S â†’â‚€ A), hÎ±âŸ©
  dsimp only [ModuleCat.hom_comp] at hÎ±
  rcases (moduleCat_pOpcycles_eq_iff _ _ _).1 hÎ± with âŸ¨(Î´ : G Ã— G â†’â‚€ A), hÎ²âŸ©
/- Then, by assumption, `d(W + Î´) = Câ‚(i, Id)(Î± + Z) - x`. -/
  have hÎ±Z : dâ‚‚â‚ A (W + Î´) = mapDomain Subtype.val (Î± + Z) - x := by
    simp_all only [shortComplexH1, Finsupp.coe_sub, ModuleCat.hom_ofHom, Action.res_obj_V,
      Subgroup.coe_subtype, lmapDomain_apply, Finsupp.coe_add, Pi.sub_apply, Pi.add_apply,
      mapShortComplexH1_Ï„â‚‚, ModuleCat.ofHom_comp, Action.id_hom, ModuleCat.hom_id,
      mapRange.linearMap_id, ModuleCat.ofHom_id, Category.comp_id, LinearMap.coe_comp,
      Function.comp_apply, coinvariantsShortComplex_Xâ‚, Submodule.coe_subtype,
      coinvariantsShortComplex_f, MonoidHom.coe_id, lmapDomain_id, subtype_hom, Category.id_comp,
      mapRange.linearMap_apply, map_sub, map_add, â† sub_add, â† sub_sub, sub_add_eq_add_sub,
      add_sub_cancel, mapDomain_add, b]
/- So we claim that `Î± + Z` is an element of `Zâ‚(S, A)` which differs from `x` by a boundary in
`Zâ‚(G, A)`. -/
  use H1Ï€ _ âŸ¨Î± + Z, ?_âŸ©
/- Indeed, by `hÎ±Z`, `d(W + Î´)` is the desired boundary: -/
  Â· simp only [H1CoresCoinf_Xâ‚‚, H1CoresCoinf_Xâ‚, H1CoresCoinf_f, H1Ï€_comp_map_apply]
    refine (H1Ï€_eq_iff _ _).2 âŸ¨W + Î´, ?_âŸ©
    simp only [hÎ±Z, Action.res_obj_V, mapCyclesâ‚_hom, ModuleCat.ofHom_comp, Subgroup.coe_subtype,
      Action.id_hom, ModuleCat.hom_id, mapRange.linearMap_id, ModuleCat.ofHom_id, Category.comp_id,
      ModuleCat.hom_ofHom, LinearMap.restrict_coe_apply, lmapDomain_apply]
/- And `Î± + Z` is a cycle, since `d(W + Î´) + x` is. -/
  Â· rw [mem_cyclesâ‚_iff]
    have : x + dâ‚‚â‚ A (W + Î´) âˆˆ cyclesâ‚ A := Submodule.add_mem _ x.2 (dâ‚‚â‚_apply_mem_cyclesâ‚ _)
    rwa [eq_sub_iff_add_eq'.1 hÎ±Z, mem_cyclesâ‚_iff, sum_mapDomain_index_inj
      Subtype.val_injective, sum_mapDomain_index_inj Subtype.val_injective] at this

end CoresCoinf

end H1

section H2

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map from the short complex
`(G Ã— G Ã— G â†’â‚€ A) --dâ‚ƒâ‚‚--> (G Ã— G â†’â‚€ A) --dâ‚‚â‚--> (G â†’â‚€ A)` to
`(H Ã— H Ã— H â†’â‚€ B) --dâ‚ƒâ‚‚--> (H Ã— H â†’â‚€ B) --dâ‚‚â‚--> (H â†’â‚€ B)`. -/
@[simps]
noncomputable def mapShortComplexH2 :
    shortComplexH2 A âŸ¶ shortComplexH2 B where
  Ï„â‚ := chainsMapâ‚ƒ f Ï†
  Ï„â‚‚ := chainsMapâ‚‚ f Ï†
  Ï„â‚ƒ := chainsMapâ‚ f Ï†
  commâ‚â‚‚ := by
    simp only [shortComplexH2]
    ext : 3
    simpa [dâ‚ƒâ‚‚, map_add, map_sub, â† map_inv]
      using congr(Finsupp.single _ $((hom_comm_apply Ï† _ _).symm))
  commâ‚‚â‚ƒ := by
    simp only [shortComplexH2]
    ext : 3
    simpa [dâ‚‚â‚, map_add, map_sub, â† map_inv]
      using congr(Finsupp.single _ $((hom_comm_apply Ï† _ _).symm))

@[simp]
theorem mapShortComplexH2_zero :
    mapShortComplexH2 (A := A) (B := B) f 0 = 0 := by
  refine ShortComplex.hom_ext _ _ ?_ ?_ ?_
  all_goals
  { simp only [shortComplexH2]
    ext
    simp }

@[simp]
theorem mapShortComplexH2_id : mapShortComplexH2 (MonoidHom.id _) (ğŸ™ A) = ğŸ™ _ := by
  refine ShortComplex.hom_ext _ _ ?_ ?_ ?_
  all_goals
  { simp only [shortComplexH2]
    ext
    simp }

theorem mapShortComplexH2_comp {G H K : Type u} [Group G] [Group H] [Group K]
    {A : Rep k G} {B : Rep k H} {C : Rep k K} (f : G â†’* H) (g : H â†’* K)
    (Ï† : A âŸ¶ (Action.res _ f).obj B) (Ïˆ : B âŸ¶ (Action.res _ g).obj C) :
    mapShortComplexH2 (g.comp f) (Ï† â‰« (Action.res _ f).map Ïˆ) =
      (mapShortComplexH2 f Ï†) â‰« (mapShortComplexH2 g Ïˆ) := by
  refine ShortComplex.hom_ext _ _ ?_ ?_ ?_
  all_goals
  { simp only [shortComplexH2]
    ext
    simp [Prod.map] }

theorem mapShortComplexH2_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) :
    mapShortComplexH2 (MonoidHom.id G) (Ï† â‰« Ïˆ) =
      mapShortComplexH2 (MonoidHom.id G) Ï† â‰« mapShortComplexH2 (MonoidHom.id G) Ïˆ :=
  mapShortComplexH2_comp (MonoidHom.id G) (MonoidHom.id G) _ _

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map `Zâ‚‚(G, A) âŸ¶ Zâ‚‚(H, B)`. -/
noncomputable abbrev mapCyclesâ‚‚ :
    ModuleCat.of k (cyclesâ‚‚ A) âŸ¶ ModuleCat.of k (cyclesâ‚‚ B) :=
  ShortComplex.cyclesMap' (mapShortComplexH2 f Ï†) (shortComplexH2 A).moduleCatLeftHomologyData
    (shortComplexH2 B).moduleCatLeftHomologyData

lemma mapCyclesâ‚‚_hom :
    (mapCyclesâ‚‚ f Ï†).hom = (chainsMapâ‚‚ f Ï†).hom.restrict (fun x _ => by
      have := congr($((mapShortComplexH2 f Ï†).commâ‚‚â‚ƒ) x); simp_all [cyclesâ‚‚, shortComplexH2]) :=
  rfl

@[reassoc, elementwise]
lemma mapCyclesâ‚‚_comp {G H K : Type u} [Group G] [Group H] [Group K]
    {A : Rep k G} {B : Rep k H} {C : Rep k K} (f : G â†’* H) (g : H â†’* K)
    (Ï† : A âŸ¶ (Action.res _ f).obj B) (Ïˆ : B âŸ¶ (Action.res _ g).obj C) :
    mapCyclesâ‚‚ (g.comp f) (Ï† â‰« (Action.res _ f).map Ïˆ) =
      mapCyclesâ‚‚ f Ï† â‰« mapCyclesâ‚‚ g Ïˆ := by
  rw [â† cyclesMap'_comp, â† mapShortComplexH2_comp]

@[reassoc, elementwise]
theorem mapCyclesâ‚‚_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) :
    mapCyclesâ‚‚ (MonoidHom.id G) (Ï† â‰« Ïˆ) =
      mapCyclesâ‚‚ (MonoidHom.id G) Ï† â‰« mapCyclesâ‚‚ (MonoidHom.id G) Ïˆ :=
  mapCyclesâ‚‚_comp (MonoidHom.id G) (MonoidHom.id G) _ _

@[reassoc, elementwise]
lemma mapCyclesâ‚‚_comp_i :
    mapCyclesâ‚‚ f Ï† â‰« (shortComplexH2 B).moduleCatLeftHomologyData.i =
      (shortComplexH2 A).moduleCatLeftHomologyData.i â‰« chainsMapâ‚‚ f Ï† := by
  simp

@[simp]
lemma coe_mapCyclesâ‚‚ (x) :
    (mapCyclesâ‚‚ f Ï† x).1 = chainsMapâ‚‚ f Ï† x := rfl

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma cyclesMap_comp_isoCyclesâ‚‚_hom :
    cyclesMap f Ï† 2 â‰« (isoCyclesâ‚‚ B).hom = (isoCyclesâ‚‚ A).hom â‰« mapCyclesâ‚‚ f Ï† := by
  simp [â† cancel_mono (moduleCatLeftHomologyData (shortComplexH2 B)).i, mapShortComplexH2,
    chainsMap_f_2_comp_chainsIsoâ‚‚ f]

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma H2Ï€_comp_map :
    H2Ï€ A â‰« map f Ï† 2 = mapCyclesâ‚‚ f Ï† â‰« H2Ï€ B := by
  simp [H2Ï€, Iso.inv_comp_eq, â† cyclesMap_comp_isoCyclesâ‚‚_hom_assoc]

end H2

variable (k G) in
/-- The functor sending a representation to its complex of inhomogeneous chains. -/
@[simps]
noncomputable def chainsFunctor :
    Rep k G â¥¤ ChainComplex (ModuleCat k) â„• where
  obj A := inhomogeneousChains A
  map f := chainsMap (MonoidHom.id _) f
  map_id _ := chainsMap_id
  map_comp Ï† Ïˆ := chainsMap_comp (MonoidHom.id G) (MonoidHom.id G) Ï† Ïˆ

instance : (chainsFunctor k G).PreservesZeroMorphisms where

variable (k G) in
/-- The functor sending a `G`-representation `A` to `Hâ‚™(G, A)`. -/
@[simps]
noncomputable def functor (n : â„•) : Rep k G â¥¤ ModuleCat k where
  obj A := groupHomology A n
  map {A B} Ï† := map (MonoidHom.id _) Ï† n
  map_id A := by simp [map, groupHomology]
  map_comp f g := by
    simp only [â† HomologicalComplex.homologyMap_comp, â† chainsMap_comp]
    rfl

instance (n : â„•) : (functor k G n).PreservesZeroMorphisms where
  map_zero _ _ := by simp [map]

end groupHomology
