/-
Copyright (c) 2024 Amelia Livingston. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Amelia Livingston
-/
import Mathlib.RepresentationTheory.Homological.GroupHomology.Basic

/-!
# Functoriality of group homology
Given a commutative ring `k`, a group homomorphism `f : G â†’* H`, a `k`-linear `G`-representation
`A`, a `k`-linear `H`-representation `B`, and a representation morphism `A âŸ¶ Res(f)(B)`, we get
a chain map `inhomogeneousChains A âŸ¶ inhomogeneousChains B` and hence maps on homology
`Hâ‚™(G, A) âŸ¶ Hâ‚™(H, B)`. We use this to show a short exact sequence of representations induces a
short exact sequence of complexes of inhomogeneous chains, allowing us to specialize API for long
exact sequences to group homology.
We also provide extra API for these functoriality maps in degrees 0, 1, 2.
## Main definitions
* `groupHomology.chainsMap f Ï†` is the map `inhomogeneousChains A âŸ¶ inhomogeneousChains B`
induced by a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`.
* `groupHomology.map f Ï† n` is the map `Hâ‚™(G, A) âŸ¶ Hâ‚™(H, B)` induced by a group homomorphism
`f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`.
-/

universe v u
variable (n : â„•)

open CategoryTheory

@[simp]
lemma QuotientGroup.mk'_comp_subtype {G : Type*} [Group G] (H : Subgroup G) [H.Normal] :
    (QuotientGroup.mk' H).comp H.subtype = 1 := by
  ext
  simp

lemma Fin.comp_contractNth {G H : Type*} [MulOneClass G] [MulOneClass H] (f : G â†’* H)
    (j : Fin (n + 1)) (g : Fin (n + 1) â†’ G) :
    f âˆ˜ Fin.contractNth j (Â· * Â·) g = Fin.contractNth j (Â· * Â·) (f âˆ˜ g) := by
  ext x
  rcases lt_trichotomy (x : â„•) j with (h|h|h)
  Â· simp only [Function.comp_apply, Fin.contractNth_apply_of_lt, h]
  Â· simp only [Function.comp_apply, Fin.contractNth_apply_of_eq, h, f.map_mul]
  Â· simp only [Function.comp_apply, Fin.contractNth_apply_of_gt, h]

namespace Finsupp

/-- Given a family `Sáµ¢` of `R`-submodules of `M` indexed by a type `Î±`, this is the `R`-submodule
of `Î± â†’â‚€ M` of functions `f` such that `f i âˆˆ Sáµ¢` for all `i : Î±`. -/
def submodule {R M Î± : Type*} [Semiring R] [AddCommMonoid M] [Module R M]
    (S : Î± â†’ Submodule R M) : Submodule R (Î± â†’â‚€ M) where
  carrier := { x | âˆ€ i, x i âˆˆ S i }
  add_mem' hx hy i := (S i).add_mem (hx i) (hy i)
  zero_mem' i := (S i).zero_mem
  smul_mem' r _ hx i := (S i).smul_mem r (hx i)

@[simp]
lemma mem_submodule_iff {R M Î± : Type*} [Semiring R] [AddCommMonoid M] [Module R M]
    (S : Î± â†’ Submodule R M) (x : Î± â†’â‚€ M) :
    x âˆˆ Finsupp.submodule S â†” âˆ€ i, x i âˆˆ S i := by
  rfl

theorem ker_mapRange {R M N : Type*} [CommSemiring R] [AddCommMonoid M] [AddCommMonoid N]
    [Module R M] [Module R N] (f : M â†’â‚—[R] N) (I : Type*) :
    LinearMap.ker (Finsupp.mapRange.linearMap (Î± := I) f) =
      Finsupp.submodule (fun _ => LinearMap.ker f) := by
  ext x
  simp [Finsupp.ext_iff]

theorem mapRange_linearMap_comp_lsingle
    {R M N : Type*} [CommSemiring R] [AddCommMonoid M] [AddCommMonoid N]
    [Module R M] [Module R N] (f : M â†’â‚—[R] N) {I : Type*} (i : I) :
    Finsupp.mapRange.linearMap f âˆ˜â‚— Finsupp.lsingle i = Finsupp.lsingle i âˆ˜â‚— f := by
  ext x y
  simp

theorem mapRange_injective_iff {Î± M N : Type*} [Zero M] [Zero N] [Nonempty Î±]
    (f : M â†’ N) (hf : f 0 = 0) :
    (mapRange (Î± := Î±) f hf).Injective â†” Function.Injective f :=
  âŸ¨fun h x y hxy => Nonempty.elim (Î± := Î±) inferInstance fun a => by
    simpa using congr($(@h (Finsupp.single a x) (Finsupp.single a y)
      (by simp only [hxy, mapRange_single])) a),
  fun h _ _ hxy => Finsupp.ext fun i => h <| by simpa using congr($hxy i)âŸ©

lemma mapDomain_surjective {Î± Î² M : Type*} [AddCommMonoid M] (f : Î± â†’ Î²) (hf : f.Surjective) :
    (mapDomain (M := M) f).Surjective := by
  intro x
  induction' x using Finsupp.induction with b m x _ _ hy
  Â· use 0
    rw [mapDomain_zero]
  Â· rcases hy with âŸ¨y, rflâŸ©
    rcases hf b with âŸ¨a, rflâŸ©
    use single a m + y
    rw [mapDomain_add, mapDomain_single]

theorem range_mapRange_linearMap
    {R M N : Type*} [CommSemiring R] [AddCommMonoid M] [AddCommMonoid N]
    [Module R M] [Module R N] (f : M â†’â‚—[R] N) (hf : LinearMap.ker f = âŠ¥) (I : Type*) :
    LinearMap.range (Finsupp.mapRange.linearMap (Î± := I) f)
      = Finsupp.submodule (fun _ => LinearMap.range f) := by
  ext x
  constructor
  Â· rintro âŸ¨y, hyâŸ©
    simp [â† hy]
  Â· intro hx
    choose y hy using hx
    refine âŸ¨âŸ¨x.support, y, fun i => ?_âŸ©, by ext; simp_allâŸ©
    constructor
    <;> contrapose!
    <;> simp_all (config := {contextual := true}) [â† hy, map_zero, LinearMap.ker_eq_bot'.1 hf]

end Finsupp

namespace groupHomology
open Rep Finsupp

variable {k G H : Type u} [CommRing k] [Group G] [Group H]
  {A : Rep k G} {B : Rep k H} (f : G â†’* H) (Ï† : A âŸ¶ (Action.res _ f).obj B) (n : â„•)

open Representation

variable (S : Subgroup G)

variable [DecidableEq G] [DecidableEq H]

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the chain map sending `âˆ‘ aáµ¢ â€¢ gáµ¢ : (Fin n â†’ G) â†’â‚€ A)` to
`âˆ‘ Ï†(aáµ¢) â€¢ (f âˆ˜ gáµ¢)) : (Fin n â†’ H) â†’â‚€ B`. -/
@[simps (config := .lemmasOnly) f f_hom]
noncomputable def chainsMap :
    inhomogeneousChains A âŸ¶ inhomogeneousChains B where
  f i := ModuleCat.ofHom <| mapRange.linearMap Ï†.hom.hom âˆ˜â‚— lmapDomain A k (f âˆ˜ Â·)
  comm' i j (hij : _ = _) := by
    subst hij
    refine ModuleCat.hom_ext <| lhom_ext fun g a => ?_
    simpa [Fin.comp_contractNth, map_add] using
      congr(single (fun (k : Fin j) => f (g k.succ)) $((hom_comm_apply Ï† (g 0)â»Â¹ a).symm))

@[reassoc (attr := simp)]
lemma lsingle_comp_chainsMap (n : â„•) (x : Fin n â†’ G) :
    ModuleCat.ofHom (lsingle x) â‰« (chainsMap f Ï†).f n =
      Ï†.hom â‰« ModuleCat.ofHom (lsingle (f âˆ˜ x)) := by
  ext
  simp [chainsMap_f]

lemma chainsMap_f_single (n : â„•) (x : Fin n â†’ G) (a : A) :
    (chainsMap f Ï†).f n (single x a) = single (f âˆ˜ x) (Ï†.hom a) := by
  simp [chainsMap_f]

@[simp]
lemma chainsMap_id :
    chainsMap (MonoidHom.id G) (ğŸ™ A) = ğŸ™ (inhomogeneousChains A) := by
  ext : 1
  exact ModuleCat.hom_ext <| lhom_ext' fun _ =>
    ModuleCat.hom_ext_iff.1 <| lsingle_comp_chainsMap (k := k) (MonoidHom.id G) _ _ _

@[simp]
lemma chainsMap_id_eq_mapRange {A B : Rep k G} (i : â„•) (Ï† : A âŸ¶ B) :
    (chainsMap (MonoidHom.id G) Ï†).f i = ModuleCat.ofHom (mapRange.linearMap Ï†.hom.hom) := by
  refine ModuleCat.hom_ext <| lhom_ext fun _ _ => ?_
  simp [chainsMap_f, MonoidHom.coe_id]

@[simp]
lemma chainsMap_comp {G H K : Type u} [Group G] [Group H] [Group K]
    [DecidableEq G] [DecidableEq H] [DecidableEq K] {A : Rep k G} {B : Rep k H} {C : Rep k K}
    (f : G â†’* H) (g : H â†’* K) (Ï† : A âŸ¶ (Action.res _ f).obj B) (Ïˆ : B âŸ¶ (Action.res _ g).obj C) :
    chainsMap (g.comp f) (Ï† â‰« (Action.res _ f).map Ïˆ) = chainsMap f Ï† â‰« chainsMap g Ïˆ := by
  ext : 1
  refine ModuleCat.hom_ext <| lhom_ext fun _ _ => ?_
  simp [chainsMap_f, Function.comp_assoc]

@[simp]
lemma chainsMap_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) :
    chainsMap (MonoidHom.id G) (Ï† â‰« Ïˆ) =
      chainsMap (MonoidHom.id G) Ï† â‰« chainsMap (MonoidHom.id G) Ïˆ :=
  chainsMap_comp (MonoidHom.id G) (MonoidHom.id G) _ _

@[simp]
lemma chainsMap_zero : chainsMap f (0 : A âŸ¶ (Action.res _ f).obj B) = 0 :=
  HomologicalComplex.hom_ext _ _ <| fun i => ModuleCat.hom_ext <| lhom_ext' <|
    fun x => LinearMap.ext fun (y : A) => by simp [chainsMap_f, LinearMap.zero_apply (Mâ‚‚ := B)]

lemma chainsMap_f_map_mono (hf : Function.Injective f) [Mono Ï†] (i : â„•) :
    Mono ((chainsMap f Ï†).f i) := by
  simpa [ModuleCat.mono_iff_injective] using
    ((mapRange_injective_iff Ï†.hom (map_zero _)).2 <| (Rep.mono_iff_injective Ï†).1
    inferInstance).comp (mapDomain_injective hf.comp_left)

instance chainsMap_id_f_map_mono {A B : Rep k G} (Ï† : A âŸ¶ B) [Mono Ï†] (i : â„•) :
    Mono ((chainsMap (MonoidHom.id G) Ï†).f i) :=
  chainsMap_f_map_mono (MonoidHom.id G) Ï† (fun _ _ h => h) _

lemma chainsMap_f_map_epi (hf : Function.Surjective f) [Epi Ï†] (i : â„•) :
    Epi ((chainsMap f Ï†).f i) := by
  simpa [ModuleCat.epi_iff_surjective] using
    (mapRange_surjective Ï†.hom (map_zero _) ((Rep.epi_iff_surjective Ï†).1 inferInstance)).comp
    (mapDomain_surjective _ hf.comp_left)

instance chainsMap_id_f_map_epi {A B : Rep k G} (Ï† : A âŸ¶ B) [Epi Ï†] (i : â„•) :
    Epi ((chainsMap (MonoidHom.id G) Ï†).f i) :=
  chainsMap_f_map_epi _ _ (fun x => âŸ¨x, rflâŸ©) _

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map `Zâ‚™(G, A) âŸ¶ Zâ‚™(H, B)` sending `âˆ‘ aáµ¢ â€¢ gáµ¢ : (Fin n â†’ G) â†’â‚€ A)` to
`âˆ‘ Ï†(aáµ¢) â€¢ (f âˆ˜ gáµ¢) : (Fin n â†’ H) â†’â‚€ B`. -/
noncomputable abbrev cyclesMap (n : â„•) :
    groupHomology.cycles A n âŸ¶ groupHomology.cycles B n :=
  HomologicalComplex.cyclesMap (chainsMap f Ï†) n

@[simp]
theorem cyclesMap_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) (n : â„•) :
    cyclesMap (MonoidHom.id G) (Ï† â‰« Ïˆ) n =
      cyclesMap (MonoidHom.id G) Ï† n â‰« cyclesMap (MonoidHom.id G) Ïˆ n := by
  simp [cyclesMap, chainsMap_id_comp, HomologicalComplex.cyclesMap_comp]

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map `Hâ‚™(G, A) âŸ¶ Hâ‚™(H, B)` sending `âˆ‘ aáµ¢ â€¢ gáµ¢ : (Fin n â†’ G) â†’â‚€ A)` to
`âˆ‘ Ï†(aáµ¢) â€¢ (f âˆ˜ gáµ¢) : (Fin n â†’ H) â†’â‚€ B`. -/
noncomputable abbrev map (n : â„•) :
  groupHomology A n âŸ¶ groupHomology B n :=
HomologicalComplex.homologyMap (chainsMap f Ï†) n

@[simp]
theorem map_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) (n : â„•) :
    map (MonoidHom.id G) (Ï† â‰« Ïˆ) n =
      map (MonoidHom.id G) Ï† n â‰« map (MonoidHom.id G) Ïˆ n := by
  rw [map, chainsMap_id_comp, HomologicalComplex.homologyMap_comp]

end groupHomology
