/-
Copyright (c) 2025 Amelia Livingston. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Amelia Livingston
-/
import Mathlib.RepresentationTheory.Homological.GroupHomology.LowDegree
import Mathlib.RepresentationTheory.Homological.GroupCohomology.ToMove

/-!
# Functoriality of group homology

Given a commutative ring `k`, a group homomorphism `f : G â†’* H`, a `k`-linear `G`-representation
`A`, a `k`-linear `H`-representation `B`, and a representation morphism `A âŸ¶ Res(f)(B)`, we get
a chain map `inhomogeneousChains A âŸ¶ inhomogeneousChains B` and hence maps on homology
`Hâ‚™(G, A) âŸ¶ Hâ‚™(H, B)`.

We also provide extra API for these maps in degrees 0, 1, 2.

## Main definitions

* `groupHomology.chainsMap f Ï†` is the map `inhomogeneousChains A âŸ¶ inhomogeneousChains B`
induced by a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`.
* `groupHomology.map f Ï† n` is the map `Hâ‚™(G, A) âŸ¶ Hâ‚™(H, B)` induced by a group homomorphism
`f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`.

-/

universe v u

namespace groupHomology

open CategoryTheory Rep Finsupp Representation

variable {k G H : Type u} [CommRing k] [Group G] [Group H]
  {A : Rep k G} {B : Rep k H} (f : G â†’* H) (Ï† : A âŸ¶ (Action.res _ f).obj B) (n : â„•)

theorem congr {fâ‚ fâ‚‚ : G â†’* H} (h : fâ‚ = fâ‚‚) {Ï† : A âŸ¶ (Action.res _ fâ‚).obj B} {T : Type*}
    (F : (f : G â†’* H) â†’ (Ï† : A âŸ¶ (Action.res _ f).obj B) â†’ T) :
    F fâ‚ Ï† = F fâ‚‚ (h â–¸ Ï†) := by
  subst h
  rfl

variable [DecidableEq G] [DecidableEq H]

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the chain map sending `âˆ‘ aáµ¢Â·gáµ¢ : Gâ¿ â†’â‚€ A` to
`âˆ‘ Ï†(aáµ¢)Â·(f âˆ˜ gáµ¢) : Hâ¿ â†’â‚€ B`. -/
@[simps! (config := .lemmasOnly) f f_hom]
noncomputable def chainsMap :
    inhomogeneousChains A âŸ¶ inhomogeneousChains B where
  f i := ModuleCat.ofHom <| mapRange.linearMap Ï†.hom.hom âˆ˜â‚— lmapDomain A k (f âˆ˜ Â·)
  comm' i j (hij : _ = _) := by
    subst hij
    refine ModuleCat.hom_ext <| lhom_ext fun _ _ => ?_
    simpa [Fin.comp_contractNth, map_add, res_obj_Ï] using
      congr(single _ $((hom_comm_apply Ï† (_)â»Â¹ _).symm))

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma lsingle_comp_chainsMap_f (n : â„•) (x : Fin n â†’ G) :
    ModuleCat.ofHom (lsingle x) â‰« (chainsMap f Ï†).f n =
      Ï†.hom â‰« ModuleCat.ofHom (lsingle (f âˆ˜ x)) := by
  ext
  simp [chainsMap_f]

lemma chainsMap_f_single (n : â„•) (x : Fin n â†’ G) (a : A) :
    (chainsMap f Ï†).f n (single x a) = single (f âˆ˜ x) (Ï†.hom a) := by
  simp [chainsMap_f]

@[simp]
lemma chainsMap_id :
    chainsMap (MonoidHom.id G) (ğŸ™ A) = ğŸ™ (inhomogeneousChains A) :=
  HomologicalComplex.hom_ext _ _ fun _ => ModuleCat.hom_ext <| lhom_ext' fun _ =>
    ModuleCat.hom_ext_iff.1 <| lsingle_comp_chainsMap_f (k := k) (MonoidHom.id G) ..

@[simp]
lemma chainsMap_f_id_eq_mapRange {A B : Rep k G} (i : â„•) (Ï† : A âŸ¶ B) :
    (chainsMap (MonoidHom.id G) Ï†).f i = ModuleCat.ofHom (mapRange.linearMap Ï†.hom.hom) := by
  refine ModuleCat.hom_ext <| lhom_ext fun _ _ => ?_
  simp [chainsMap_f, MonoidHom.coe_id]

lemma chainsMap_comp {G H K : Type u} [Group G] [Group H] [Group K]
    [DecidableEq G] [DecidableEq H] [DecidableEq K] {A : Rep k G} {B : Rep k H} {C : Rep k K}
    (f : G â†’* H) (g : H â†’* K) (Ï† : A âŸ¶ (Action.res _ f).obj B) (Ïˆ : B âŸ¶ (Action.res _ g).obj C) :
    chainsMap (g.comp f) (Ï† â‰« (Action.res _ f).map Ïˆ) = chainsMap f Ï† â‰« chainsMap g Ïˆ := by
  ext : 1
  refine ModuleCat.hom_ext <| lhom_ext fun _ _ => ?_
  simp [chainsMap_f, Function.comp_assoc]

lemma chainsMap_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) :
    chainsMap (MonoidHom.id G) (Ï† â‰« Ïˆ) =
      chainsMap (MonoidHom.id G) Ï† â‰« chainsMap (MonoidHom.id G) Ïˆ :=
  chainsMap_comp (MonoidHom.id G) (MonoidHom.id G) _ _

@[simp]
lemma chainsMap_zero : chainsMap f (0 : A âŸ¶ (Action.res _ f).obj B) = 0 :=
  HomologicalComplex.hom_ext _ _ <| fun _ => ModuleCat.hom_ext <| lhom_ext' <|
    fun _ => LinearMap.ext fun _ => by simp [chainsMap_f, LinearMap.zero_apply (Mâ‚‚ := B)]

lemma chainsMap_f_map_mono (hf : Function.Injective f) [Mono Ï†] (i : â„•) :
    Mono ((chainsMap f Ï†).f i) := by
  simpa [ModuleCat.mono_iff_injective] using
    (mapRange_injective Ï†.hom (map_zero _) <| (Rep.mono_iff_injective Ï†).1
    inferInstance).comp (mapDomain_injective hf.comp_left)

instance chainsMap_id_f_map_mono {A B : Rep k G} (Ï† : A âŸ¶ B) [Mono Ï†] (i : â„•) :
    Mono ((chainsMap (MonoidHom.id G) Ï†).f i) :=
  chainsMap_f_map_mono (MonoidHom.id G) Ï† (fun _ _ h => h) _

lemma chainsMap_f_map_epi (hf : Function.Surjective f) [Epi Ï†] (i : â„•) :
    Epi ((chainsMap f Ï†).f i) := by
  simpa [ModuleCat.epi_iff_surjective] using
    (mapRange_surjective Ï†.hom (map_zero _) ((Rep.epi_iff_surjective Ï†).1 inferInstance)).comp
    (mapDomain_surjective hf.comp_left)

instance chainsMap_id_f_map_epi {A B : Rep k G} (Ï† : A âŸ¶ B) [Epi Ï†] (i : â„•) :
    Epi ((chainsMap (MonoidHom.id G) Ï†).f i) :=
  chainsMap_f_map_epi _ _ (fun x => âŸ¨x, rflâŸ©) _

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map `Zâ‚™(G, A) âŸ¶ Zâ‚™(H, B)` sending `âˆ‘ aáµ¢Â·gáµ¢ : Gâ¿ â†’â‚€ A` to
`âˆ‘ Ï†(aáµ¢)Â·(f âˆ˜ gáµ¢) : Hâ¿ â†’â‚€ B`. -/
noncomputable abbrev cyclesMap (n : â„•) :
    groupHomology.cycles A n âŸ¶ groupHomology.cycles B n :=
  HomologicalComplex.cyclesMap (chainsMap f Ï†) n

theorem cyclesMap_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) (n : â„•) :
    cyclesMap (MonoidHom.id G) (Ï† â‰« Ïˆ) n =
      cyclesMap (MonoidHom.id G) Ï† n â‰« cyclesMap (MonoidHom.id G) Ïˆ n := by
  simp [cyclesMap, chainsMap_id_comp, HomologicalComplex.cyclesMap_comp]

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map `Hâ‚™(G, A) âŸ¶ Hâ‚™(H, B)` sending `âˆ‘ aáµ¢Â·gáµ¢ : Gâ¿ â†’â‚€ A` to
`âˆ‘ Ï†(aáµ¢)Â·(f âˆ˜ gáµ¢) : Hâ¿ â†’â‚€ B`. -/
noncomputable abbrev map (n : â„•) :
    groupHomology A n âŸ¶ groupHomology B n :=
  HomologicalComplex.homologyMap (chainsMap f Ï†) n

theorem map_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) (n : â„•) :
    map (MonoidHom.id G) (Ï† â‰« Ïˆ) n =
      map (MonoidHom.id G) Ï† n â‰« map (MonoidHom.id G) Ïˆ n := by
  rw [map, chainsMap_id_comp, HomologicalComplex.homologyMap_comp]

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map sending `âˆ‘ aáµ¢Â·gáµ¢ : G â†’â‚€ A` to `âˆ‘ Ï†(aáµ¢)Â·f(gáµ¢) : H â†’â‚€ B` -/
noncomputable abbrev fOne : (G â†’â‚€ A) â†’â‚—[k] H â†’â‚€ B :=
  mapRange.linearMap Ï†.hom.hom âˆ˜â‚— lmapDomain A k f

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map sending `âˆ‘ aáµ¢Â·(gáµ¢â‚, gáµ¢â‚‚) : G Ã— G â†’â‚€ A` to
`âˆ‘ Ï†(aáµ¢)Â·(f(gáµ¢â‚), f(gáµ¢â‚‚)) : H Ã— H â†’â‚€ B`. -/
noncomputable abbrev fTwo : (G Ã— G â†’â‚€ A) â†’â‚—[k] H Ã— H â†’â‚€ B :=
  mapRange.linearMap Ï†.hom.hom âˆ˜â‚— lmapDomain A k (Prod.map f f)

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map sending `âˆ‘ aáµ¢Â·(gáµ¢â‚, gáµ¢â‚‚, gáµ¢â‚ƒ) : G Ã— G Ã— G â†’â‚€ A` to
`âˆ‘ Ï†(aáµ¢)Â·(f(gáµ¢â‚), f(gáµ¢â‚‚), f(gáµ¢â‚ƒ)) : H Ã— H Ã— H â†’â‚€ B`. -/
noncomputable abbrev fThree : (G Ã— G Ã— G â†’â‚€ A) â†’â‚—[k] H Ã— H Ã— H â†’â‚€ B :=
  mapRange.linearMap Ï†.hom.hom âˆ˜â‚— lmapDomain A k (Prod.map f (Prod.map f f))

@[reassoc]
lemma chainsMap_f_0_comp_zeroChainsIso :
    (chainsMap f Ï†).f 0 â‰« (zeroChainsIso B).hom = (zeroChainsIso A).hom â‰« Ï†.hom := by
  ext
  simp [zeroChainsIso, Unique.eq_default]

@[reassoc]
lemma chainsMap_f_1_comp_oneChainsIso :
    (chainsMap f Ï†).f 1 â‰« (oneChainsIso B).hom =
      (oneChainsIso A).hom â‰« ModuleCat.ofHom (fOne f Ï†) := by
  ext
  simp [oneChainsIso, fOne]

@[reassoc]
lemma chainsMap_f_2_comp_twoChainsIso :
    (chainsMap f Ï†).f 2 â‰« (twoChainsIso B).hom =
      (twoChainsIso A).hom â‰« ModuleCat.ofHom (fTwo f Ï†) := by
  ext
  simp [twoChainsIso, fTwo]

@[reassoc]
lemma chainsMap_f_3_comp_threeChainsIso :
    (chainsMap f Ï†).f 3 â‰« (threeChainsIso B).hom =
      (threeChainsIso A).hom â‰« ModuleCat.ofHom (fThree f Ï†) := by
  ext
  simp [threeChainsIso, fThree, â† Fin.comp_tail]

open ShortComplex

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map `A_G âŸ¶ B_H`. -/
noncomputable def H0Map : H0 A âŸ¶ H0 B :=
  ModuleCat.ofHom <| Submodule.mapQ _ _ Ï†.hom.hom <| Submodule.span_le.2 <| fun _ âŸ¨âŸ¨g, yâŸ©, hyâŸ© =>
    mem_augmentationSubmodule_of_eq (f g) (Ï†.hom y) _ <| by
      simpa [â† hy] using (hom_comm_apply Ï† _ _).symm

omit [DecidableEq G] in
@[simp]
theorem H0Map_id : H0Map (MonoidHom.id G) (ğŸ™ A) = ğŸ™ _ :=
  ModuleCat.hom_ext <| Submodule.linearMap_qext _ rfl

theorem H0Map_comp {G H K : Type u} [Group G] [Group H] [Group K]
    {A : Rep k G} {B : Rep k H} {C : Rep k K} (f : G â†’* H) (g : H â†’* K)
    (Ï† : A âŸ¶ (Action.res _ f).obj B) (Ïˆ : B âŸ¶ (Action.res _ g).obj C) :
    H0Map (g.comp f) (Ï† â‰« (Action.res _ f).map Ïˆ) = H0Map f Ï† â‰« H0Map g Ïˆ :=
  ModuleCat.hom_ext <| Submodule.linearMap_qext _ rfl

omit [DecidableEq G] in
theorem H0Map_eq_coinvariantsMap {A B : Rep k G} (f : A âŸ¶ B) :
    H0Map (MonoidHom.id G) f = ModuleCat.ofHom (coinvariantsMap f) := by
  rfl

instance epi_H0Map_of_epi {A B : Rep k G} (f : A âŸ¶ B) [Epi f] :
    Epi (H0Map (MonoidHom.id G) f) :=
  (inferInstanceAs (Epi <| (coinvariantsFunctor k G).map f))

omit [DecidableEq G] [DecidableEq H] in
@[reassoc (attr := simp), elementwise (attr := simp)]
theorem H0Ï€_comp_H0Map :
    H0Ï€ A â‰« H0Map f Ï† = Ï†.hom â‰« H0Ï€ B := rfl

@[reassoc (attr := simp), elementwise (attr := simp)]
theorem map_comp_isoH0_hom :
    map f Ï† 0 â‰« (isoH0 B).hom = (isoH0 A).hom â‰« H0Map f Ï† := by
  simp [isoZeroCycles, â† cancel_epi (groupHomologyÏ€ _ _),
    chainsMap_f_0_comp_zeroChainsIso_assoc f Ï†, â† LinearEquiv.toModuleIso_hom]

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map from the short complex `(G Ã— G â†’â‚€ A) --dOne--> (G â†’â‚€ A) --dZero--> A`
to `(H Ã— H â†’â‚€ B) --dOne--> (H â†’â‚€ B) --dZero--> B`. -/
@[simps]
noncomputable def mapShortComplexH1 :
    shortComplexH1 A âŸ¶ shortComplexH1 B where
  Ï„â‚ := ModuleCat.ofHom (fTwo f Ï†)
  Ï„â‚‚ := ModuleCat.ofHom (fOne f Ï†)
  Ï„â‚ƒ := Ï†.hom
  commâ‚â‚‚ := ModuleCat.hom_ext <| lhom_ext fun a b => by
    simpa [dOne, map_add, map_sub, shortComplexH1, fTwo, fOne, â† map_inv]
      using congr(Finsupp.single (f a.2) $((hom_comm_apply Ï† _ _).symm))
  commâ‚‚â‚ƒ := ModuleCat.hom_ext <| lhom_ext fun a b => by
    simpa [map_add, map_sub, shortComplexH1, fOne, â† map_inv]
      using (hom_comm_apply Ï† _ _).symm

@[simp]
theorem mapShortComplexH1_zero :
    mapShortComplexH1 (A := A) (B := B) f 0 = 0 := by
  refine ShortComplex.hom_ext _ _ ?_ ?_ rfl
  all_goals
  { refine ModuleCat.hom_ext <| lhom_ext fun _ _ => ?_
    show mapRange.linearMap 0 (mapDomain _ (single _ _)) = 0
    simp [LinearMap.zero_apply (Mâ‚‚ := B)] }

@[simp]
theorem mapShortComplexH1_add (Ï† Ïˆ : A âŸ¶ (Action.res _ f).obj B) :
    mapShortComplexH1 f (Ï† + Ïˆ) = mapShortComplexH1 f Ï† + mapShortComplexH1 f Ïˆ := by
  refine ShortComplex.hom_ext _ _ ?_ ?_ rfl
  all_goals
  { refine ModuleCat.hom_ext <| lhom_ext fun _ _ => ?_
    simp [shortComplexH1] }

@[simp]
theorem mapShortComplexH1_id : mapShortComplexH1 (MonoidHom.id G) (ğŸ™ A) = ğŸ™ _ := by
  refine ShortComplex.hom_ext _ _ ?_ ?_ rfl
  all_goals
  { refine ModuleCat.hom_ext <| Finsupp.lhom_ext fun _ _ => ?_
    show Finsupp.mapRange.linearMap LinearMap.id _ = Finsupp.single _ _
    simp [MonoidHom.coe_id] }

theorem mapShortComplexH1_comp {G H K : Type u} [Group G] [Group H] [Group K]
    [DecidableEq G] [DecidableEq H] [DecidableEq K]
    {A : Rep k G} {B : Rep k H} {C : Rep k K} (f : G â†’* H) (g : H â†’* K)
    (Ï† : A âŸ¶ (Action.res _ f).obj B) (Ïˆ : B âŸ¶ (Action.res _ g).obj C) :
    mapShortComplexH1 (g.comp f) (Ï† â‰« (Action.res _ f).map Ïˆ) =
      (mapShortComplexH1 f Ï†) â‰« (mapShortComplexH1 g Ïˆ) := by
  refine ShortComplex.hom_ext _ _ ?_ ?_ rfl
  all_goals
  { refine ModuleCat.hom_ext <| lhom_ext fun _ _ => ?_
    simp [shortComplexH1, Prod.map, fTwo, fOne] }

theorem mapShortComplexH1_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) :
    mapShortComplexH1 (MonoidHom.id G) (Ï† â‰« Ïˆ) =
      mapShortComplexH1 (MonoidHom.id G) Ï† â‰« mapShortComplexH1 (MonoidHom.id G) Ïˆ :=
  mapShortComplexH1_comp (MonoidHom.id G) (MonoidHom.id G) _ _

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map `Zâ‚(G, A) âŸ¶ Zâ‚(H, B)`. -/
noncomputable abbrev mapOneCycles :
    ModuleCat.of k (oneCycles A) âŸ¶ ModuleCat.of k (oneCycles B) :=
  ShortComplex.cyclesMap' (mapShortComplexH1 f Ï†) (shortComplexH1 A).moduleCatLeftHomologyData
    (shortComplexH1 B).moduleCatLeftHomologyData

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma mapOneCycles_comp_i :
    mapOneCycles f Ï† â‰« (shortComplexH1 B).moduleCatLeftHomologyData.i =
      (shortComplexH1 A).moduleCatLeftHomologyData.i â‰« ModuleCat.ofHom (fOne f Ï†) :=
  ShortComplex.cyclesMap'_i (mapShortComplexH1 f Ï†) (moduleCatLeftHomologyData _)
    (moduleCatLeftHomologyData _)

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma cyclesMap_comp_isoOneCycles_hom :
    cyclesMap f Ï† 1 â‰« (isoOneCycles B).hom = (isoOneCycles A).hom â‰« mapOneCycles f Ï† := by
  simp [â† cancel_mono (shortComplexH1 B).moduleCatLeftHomologyData.i,
    chainsMap_f_1_comp_oneChainsIso f Ï†, â† LinearEquiv.toModuleIso_hom]

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map `Hâ‚(G, A) âŸ¶ Hâ‚(H, B)`. -/
noncomputable abbrev H1Map : H1 A âŸ¶ H1 B :=
  ShortComplex.leftHomologyMap' (mapShortComplexH1 f Ï†)
    (shortComplexH1 A).moduleCatLeftHomologyData (shortComplexH1 B).moduleCatLeftHomologyData

@[simp]
theorem H1Map_id : H1Map (MonoidHom.id G) (ğŸ™ A) = ğŸ™ _ := by
  simp only [H1Map, shortComplexH1, mapShortComplexH1_id, leftHomologyMap'_id]
  rfl

theorem H1Map_comp {G H K : Type u} [Group G] [Group H] [Group K]
    [DecidableEq G] [DecidableEq H] [DecidableEq K]
    {A : Rep k G} {B : Rep k H} {C : Rep k K} (f : G â†’* H) (g : H â†’* K)
    (Ï† : A âŸ¶ (Action.res _ f).obj B) (Ïˆ : B âŸ¶ (Action.res _ g).obj C) :
    H1Map (g.comp f) (Ï† â‰« (Action.res _ f).map Ïˆ) = H1Map f Ï† â‰« H1Map g Ïˆ := by
  simpa [H1Map, shortComplexH1, mapShortComplexH1_comp] using leftHomologyMap'_comp _ _ _ _ _

theorem H1Map_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) :
    H1Map (MonoidHom.id G) (Ï† â‰« Ïˆ) = H1Map (MonoidHom.id G) Ï† â‰« H1Map (MonoidHom.id G) Ïˆ :=
  H1Map_comp (MonoidHom.id G) (MonoidHom.id G) _ _

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma H1Ï€_comp_H1Map :
    H1Ï€ A â‰« H1Map f Ï† = mapOneCycles f Ï† â‰« H1Ï€ B :=
  leftHomologyÏ€_naturality' (mapShortComplexH1 f Ï†) _ _

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma map_comp_isoH1_hom :
    map f Ï† 1 â‰« (isoH1 B).hom = (isoH1 A).hom â‰« H1Map f Ï† := by
  simp [â† cancel_epi (groupHomologyÏ€ _ _)]

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map from the short complex
`(G Ã— G Ã— G â†’â‚€ A) --dTwo--> (G Ã— G â†’â‚€ A) --dOne--> (G â†’â‚€ A)` to
`(H Ã— H Ã— H â†’â‚€ B) --dTwo--> (H Ã— H â†’â‚€ B) --dOne--> (H â†’â‚€ B)`. -/
@[simps]
noncomputable def mapShortComplexH2 :
    shortComplexH2 A âŸ¶ shortComplexH2 B where
  Ï„â‚ := ModuleCat.ofHom (fThree f Ï†)
  Ï„â‚‚ := ModuleCat.ofHom (fTwo f Ï†)
  Ï„â‚ƒ := ModuleCat.ofHom (fOne f Ï†)
  commâ‚â‚‚ := ModuleCat.hom_ext <| lhom_ext fun a b => by
    simpa [dTwo, shortComplexH2, map_add, map_sub, fThree, fTwo, â† map_inv]
      using congr(Finsupp.single _ $((hom_comm_apply Ï† _ _).symm))
  commâ‚‚â‚ƒ := ModuleCat.hom_ext <| lhom_ext fun a b => by
    simpa [dOne, shortComplexH2, map_add, map_sub, fTwo, fOne, â† map_inv]
      using congr(Finsupp.single _ $((hom_comm_apply Ï† _ _).symm))

@[simp]
theorem mapShortComplexH2_zero :
    mapShortComplexH2 (A := A) (B := B) f 0 = 0 := by
  refine ShortComplex.hom_ext _ _ ?_ ?_ ?_
  all_goals
  { refine ModuleCat.hom_ext <| Finsupp.lhom_ext fun _ _ => ?_
    show Finsupp.mapRange.linearMap 0 (Finsupp.mapDomain _ (Finsupp.single _ _)) = 0
    simp [LinearMap.zero_apply (Mâ‚‚ := B)] }

@[simp]
theorem mapShortComplexH2_add (Ï† Ïˆ : A âŸ¶ (Action.res _ f).obj B) :
    mapShortComplexH2 f (Ï† + Ïˆ) = mapShortComplexH2 f Ï† + mapShortComplexH2 f Ïˆ := by
  refine ShortComplex.hom_ext _ _ ?_ ?_ ?_
  all_goals
  { refine ModuleCat.hom_ext <| lhom_ext fun _ _ => ?_
    simp [shortComplexH2] }

@[simp]
theorem mapShortComplexH2_id : mapShortComplexH2 (MonoidHom.id _) (ğŸ™ A) = ğŸ™ _ := by
  refine ShortComplex.hom_ext _ _ ?_ ?_ ?_
  all_goals
  { refine ModuleCat.hom_ext <| Finsupp.lhom_ext fun _ _ => ?_
    show Finsupp.mapRange.linearMap LinearMap.id _ = Finsupp.single _ _
    simp [MonoidHom.coe_id] }

theorem mapShortComplexH2_comp {G H K : Type u} [Group G] [Group H] [Group K]
    [DecidableEq G] [DecidableEq H] [DecidableEq K]
    {A : Rep k G} {B : Rep k H} {C : Rep k K} (f : G â†’* H) (g : H â†’* K)
    (Ï† : A âŸ¶ (Action.res _ f).obj B) (Ïˆ : B âŸ¶ (Action.res _ g).obj C) :
    mapShortComplexH2 (g.comp f) (Ï† â‰« (Action.res _ f).map Ïˆ) =
      (mapShortComplexH2 f Ï†) â‰« (mapShortComplexH2 g Ïˆ) := by
  refine ShortComplex.hom_ext _ _ ?_ ?_ ?_
  all_goals
  { refine ModuleCat.hom_ext <| Finsupp.lhom_ext fun _ _ => ?_
    simp [shortComplexH2, Prod.map, fThree, fTwo, fOne] }

theorem mapShortComplexH2_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) :
    mapShortComplexH2 (MonoidHom.id G) (Ï† â‰« Ïˆ) =
      mapShortComplexH2 (MonoidHom.id G) Ï† â‰« mapShortComplexH2 (MonoidHom.id G) Ïˆ :=
  mapShortComplexH2_comp (MonoidHom.id G) (MonoidHom.id G) _ _

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map `Zâ‚‚(G, A) âŸ¶ Zâ‚‚(H, B)`. -/
noncomputable abbrev mapTwoCycles :
    ModuleCat.of k (twoCycles A) âŸ¶ ModuleCat.of k (twoCycles B) :=
  ShortComplex.cyclesMap' (mapShortComplexH2 f Ï†) (shortComplexH2 A).moduleCatLeftHomologyData
    (shortComplexH2 B).moduleCatLeftHomologyData

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma mapTwoCycles_comp_i :
    mapTwoCycles f Ï† â‰« (shortComplexH2 B).moduleCatLeftHomologyData.i =
      (shortComplexH2 A).moduleCatLeftHomologyData.i â‰« ModuleCat.ofHom (fTwo f Ï†) :=
  ShortComplex.cyclesMap'_i (mapShortComplexH2 f Ï†) (moduleCatLeftHomologyData _)
    (moduleCatLeftHomologyData _)

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma cyclesMap_comp_isoTwoCycles_hom :
    cyclesMap f Ï† 2 â‰« (isoTwoCycles B).hom = (isoTwoCycles A).hom â‰« mapTwoCycles f Ï† := by
  simp [â† cancel_mono (shortComplexH2 B).moduleCatLeftHomologyData.i,
    chainsMap_f_2_comp_twoChainsIso f Ï†, â† LinearEquiv.toModuleIso_hom]

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : A âŸ¶ Res(f)(B)`,
this is the induced map `Hâ‚‚(G, A) âŸ¶ Hâ‚‚(H, B)`. -/
noncomputable abbrev H2Map : H2 A âŸ¶ H2 B :=
  ShortComplex.leftHomologyMap' (mapShortComplexH2 f Ï†)
    (shortComplexH2 A).moduleCatLeftHomologyData (shortComplexH2 B).moduleCatLeftHomologyData

@[simp]
theorem H2Map_id : H2Map (MonoidHom.id G) (ğŸ™ A) = ğŸ™ _ := by
  simp only [H2Map, shortComplexH2, mapShortComplexH2_id, leftHomologyMap'_id]
  rfl

theorem H2Map_comp {G H K : Type u} [Group G] [Group H] [Group K]
    [DecidableEq G] [DecidableEq H] [DecidableEq K]
    {A : Rep k G} {B : Rep k H} {C : Rep k K} (f : G â†’* H) (g : H â†’* K)
    (Ï† : A âŸ¶ (Action.res _ f).obj B) (Ïˆ : B âŸ¶ (Action.res _ g).obj C) :
    H2Map (g.comp f) (Ï† â‰« (Action.res _ f).map Ïˆ) = H2Map f Ï† â‰« H2Map g Ïˆ := by
  simpa [H2Map, shortComplexH2, mapShortComplexH2_comp] using leftHomologyMap'_comp _ _ _ _ _

theorem H2Map_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) :
    H2Map (MonoidHom.id G) (Ï† â‰« Ïˆ) = H2Map (MonoidHom.id G) Ï† â‰« H2Map (MonoidHom.id G) Ïˆ :=
  H2Map_comp (MonoidHom.id G) (MonoidHom.id G) _ _

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma H2Ï€_comp_H2Map :
    H2Ï€ A â‰« H2Map f Ï† = mapTwoCycles f Ï† â‰« H2Ï€ B :=
  leftHomologyÏ€_naturality' (mapShortComplexH2 f Ï†) _ _

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma map_comp_isoH2_hom :
    map f Ï† 2 â‰« (isoH2 B).hom = (isoH2 A).hom â‰« H2Map f Ï† := by
  simp [â† cancel_epi (groupHomologyÏ€ _ _), Category.assoc]

section Functors

variable (k G)

/-- The functor sending a representation to its complex of inhomogeneous chains. -/
@[simps]
noncomputable def chainsFunctor [DecidableEq G] :
    Rep k G â¥¤ ChainComplex (ModuleCat k) â„• where
  obj A := inhomogeneousChains A
  map f := chainsMap (MonoidHom.id _) f
  map_id _ := chainsMap_id
  map_comp Ï† Ïˆ := chainsMap_comp (MonoidHom.id G) (MonoidHom.id G) Ï† Ïˆ

instance : (chainsFunctor k G).PreservesZeroMorphisms where

instance : (chainsFunctor k G).Additive where

/-- The functor sending a `G`-representation `A` to `Zâ‚™(G, A)`. -/
noncomputable abbrev cyclesFunctor (n : â„•) : Rep k G â¥¤ ModuleCat k :=
  chainsFunctor k G â‹™ HomologicalComplex.cyclesFunctor _ _ n

instance (n : â„•) : (cyclesFunctor k G n).PreservesZeroMorphisms where
instance (n : â„•) : (cyclesFunctor k G n).Additive := inferInstance

/-- The functor sending a `G`-representation `A` to `Câ‚™(G, A)/Bâ‚™(G, A)`. -/
noncomputable abbrev opcyclesFunctor (n : â„•) : Rep k G â¥¤ ModuleCat k :=
  chainsFunctor k G â‹™ HomologicalComplex.opcyclesFunctor _ _ n

instance (n : â„•) : (opcyclesFunctor k G n).PreservesZeroMorphisms where
instance (n : â„•) : (opcyclesFunctor k G n).Additive := inferInstance

/-- The functor sending a `G`-representation `A` to `Hâ‚™(G, A)`. -/
noncomputable abbrev functor (n : â„•) : Rep k G â¥¤ ModuleCat k :=
  chainsFunctor k G â‹™ HomologicalComplex.homologyFunctor _ _ n

instance (n : â„•) : (functor k G n).PreservesZeroMorphisms where
  map_zero _ _ := by simp [map]

instance (n : â„•) : (functor k G n).Additive := inferInstance

section LowDegree

/-- The functor sending a `G`-representation `A` to its augmentation submodule. -/
@[simps]
noncomputable def augmentationSubmoduleFunctor : Rep k G â¥¤ ModuleCat k where
  obj X := ModuleCat.of k (augmentationSubmodule X.Ï)
  map {X Y} f := ModuleCat.ofHom (LinearMap.restrict f.hom.hom fun x hx =>
    (Submodule.span_le (p := Y.Ï.augmentationSubmodule.comap f.hom.hom)).2 (by
     rintro y âŸ¨z, rflâŸ©
     exact mem_augmentationSubmodule_of_eq z.1 (f.hom z.2) _
       (by simp [(hom_comm_apply f z.1 z.2).symm])) hx)
  map_id _ := rfl
  map_comp _ _ := rfl

/-- The functor sending a `G`-representation `A` to `Zâ‚(G, A)`, using a convenient expression
for `Zâ‚`. -/
@[simps]
noncomputable def oneCyclesFunctor : Rep k G â¥¤ ModuleCat k where
  obj X := ModuleCat.of k (oneCycles X)
  map f := mapOneCycles (MonoidHom.id G) f
  map_id _ := by simp [mapOneCycles, shortComplexH1]
  map_comp _ _ := by simp [mapOneCycles, â† mapShortComplexH1_id_comp, â† cyclesMap'_comp]

instance : (oneCyclesFunctor k G).PreservesZeroMorphisms where
instance : (oneCyclesFunctor k G).Additive where

/-- The functor sending a `G`-representation `A` to `Câ‚(G, A)/Bâ‚(G, A)`, using a convenient
expression for `Câ‚/Bâ‚`. . -/
@[simps]
noncomputable def oneOpcyclesFunctor : Rep k G â¥¤ ModuleCat k where
  obj X := (shortComplexH1 X).moduleCatRightHomologyData.Q
  map f := (rightHomologyMapData' (mapShortComplexH1 (MonoidHom.id G) f) _ _).Ï†Q
  map_id _ := by ext; simp
  map_comp _ _ := by ext : 1; simp [mapShortComplexH1_id_comp]

instance : (oneOpcyclesFunctor k G).PreservesZeroMorphisms where
  map_zero _ _ := by
    simp only [oneOpcyclesFunctor_obj, oneOpcyclesFunctor_map]
    rw [mapShortComplexH1_zero]
    ext
    simp

instance : (oneOpcyclesFunctor k G).Additive where
  map_add {_ _ _ _} := by
    simp only [oneOpcyclesFunctor_obj, oneOpcyclesFunctor_map]
    rw [mapShortComplexH1_add]
    ext
    simp

/-- The functor sending a `G`-representation `A` to `Hâ‚(G, A)`, using a convenient expression
for `Hâ‚`. . -/
@[simps]
noncomputable def H1Functor : Rep k G â¥¤ ModuleCat k where
  obj X := H1 X
  map f := H1Map (MonoidHom.id G) f
  map_comp _ _ := by rw [â† H1Map_comp, congr (MonoidHom.id_comp _) H1Map]; rfl

instance : (H1Functor k G).PreservesZeroMorphisms where
  map_zero _ _ := ModuleCat.hom_ext <| by simp [H1Map]

instance : (H1Functor k G).Additive where
  map_add := ModuleCat.hom_ext <| by simp [H1Map, mapShortComplexH1_add (MonoidHom.id G)]

/-- The functor sending a `G`-representation `A` to `Zâ‚‚(G, A)`, using a convenient expression
for `Zâ‚‚`. -/
@[simps]
noncomputable def twoCyclesFunctor : Rep k G â¥¤ ModuleCat k where
  obj X := ModuleCat.of k (twoCycles X)
  map f := mapTwoCycles (MonoidHom.id G) f
  map_id _ := by simp [mapTwoCycles, shortComplexH2]
  map_comp _ _ := by simp [mapTwoCycles, â† mapShortComplexH2_id_comp, â† cyclesMap'_comp]

instance : (twoCyclesFunctor k G).PreservesZeroMorphisms where
instance : (twoCyclesFunctor k G).Additive where

/-- The functor sending a `G`-representation `A` to `Câ‚‚(G, A)/Bâ‚‚(G, A)`, using a convenient
expression for `Câ‚‚/Bâ‚‚`. -/
@[simps]
noncomputable def twoOpcyclesFunctor : Rep k G â¥¤ ModuleCat k where
  obj X := (shortComplexH2 X).moduleCatRightHomologyData.Q
  map f := (rightHomologyMapData' (mapShortComplexH2 (MonoidHom.id G) f) _ _).Ï†Q
  map_id _ := by ext; simp
  map_comp _ _ := by ext : 1; simp [mapShortComplexH2_id_comp]

instance : (twoOpcyclesFunctor k G).PreservesZeroMorphisms where
  map_zero _ _ := by
    simp only [twoOpcyclesFunctor_obj, twoOpcyclesFunctor_map]
    rw [mapShortComplexH2_zero]
    ext
    simp

instance : (twoOpcyclesFunctor k G).Additive where
  map_add {_ _ _ _} := by
    simp only [twoOpcyclesFunctor_obj, twoOpcyclesFunctor_map]
    rw [mapShortComplexH2_add]
    ext
    simp

/-- The functor sending a `G`-representation `A` to `Hâ‚‚(G, A)`, using a convenient expression
for `Hâ‚‚`. -/
@[simps]
noncomputable def H2Functor : Rep k G â¥¤ ModuleCat k where
  obj X := H2 X
  map f := H2Map (MonoidHom.id G) f
  map_comp _ _ := by rw [â† H2Map_comp, congr (MonoidHom.id_comp _) H2Map]; rfl

instance : (H2Functor k G).PreservesZeroMorphisms where
  map_zero _ _ := ModuleCat.hom_ext <| by simp [H2Map]

instance : (H2Functor k G).Additive where
  map_add := ModuleCat.hom_ext <| by simp [H2Map, mapShortComplexH2_add (MonoidHom.id G)]

end LowDegree
section NatIsos

/-- The functor sending a `G`-representation `A` to `Hâ‚€(G, A) := A_G` is naturally isomorphic to
the general group homology functor at 0. -/
@[simps! hom_app inv_app]
noncomputable def isoCoinvariantsFunctor :
    functor k G 0 â‰… coinvariantsFunctor k G :=
  NatIso.ofComponents (fun _ => isoH0 _) fun f => by simp [H0Map_eq_coinvariantsMap]

/-- The functor sending a `G`-representation `A` to its 0th cycles is naturally isomorphic to the
forgetful functor `Rep k G â¥¤ ModuleCat k`. -/
@[simps! hom_app inv_app]
noncomputable def zeroCyclesFunctorIso :
    cyclesFunctor k G 0 â‰… Action.forget (ModuleCat k) G :=
  NatIso.ofComponents (fun A => isoZeroCycles A) fun f => by
    have := chainsMap_f_0_comp_zeroChainsIso (MonoidHom.id G) f
    simp_all [isoZeroCycles]

/-- The functor sending a `G`-representation `A` to its 0th opcycles is naturally isomorphic to the
coinvariants functor `Rep k G â¥¤ ModuleCat k`. -/
@[simps! hom_app inv_app]
noncomputable def zeroOpcyclesFunctorIso :
    opcyclesFunctor k G 0 â‰… coinvariantsFunctor k G :=
  NatIso.ofComponents (fun A => isoZeroOpcycles A) fun {X Y} f => by
    have := chainsMap_f_0_comp_zeroChainsIso_assoc (MonoidHom.id G) f (H0Ï€ _)
    simp_all [â† cancel_epi (HomologicalComplex.pOpcycles _ _), â† H0Map_eq_coinvariantsMap]

@[reassoc, elementwise]
theorem pOpcycles_comp_zeroOpcyclesFunctorIso_hom_app :
    (inhomogeneousChains A).pOpcycles 0 â‰« (zeroOpcyclesFunctorIso k G).hom.app A =
      (zeroChainsIso A).hom â‰« (shortComplexH0 A).g := by
  simp

/-- The functor sending a `G`-representation `A` to `Zâ‚(G, A)` is naturally isomorphic to the
general cycles functor at 1. -/
@[simps! hom_app inv_app]
noncomputable def isoOneCyclesFunctor :
    cyclesFunctor k G 1 â‰… oneCyclesFunctor k G :=
  NatIso.ofComponents (fun _ => isoOneCycles _) fun f => by simp

/-- The functor sending a `G`-representation `A` to `Câ‚(G, A)/Bâ‚(G, A)` is naturally isomorphic to
the general opcocycles functor at 1. -/
@[simps! hom_app inv_app]
noncomputable def isoOneOpcyclesFunctor :
    opcyclesFunctor k G 1 â‰… oneOpcyclesFunctor k G :=
  NatIso.ofComponents
    (fun A => (inhomogeneousChains A).opcyclesIsoSc' _ _ _ (by simp) (by simp) â‰ªâ‰« opcyclesMapIso
      (shortComplexH1Iso A) â‰ªâ‰« (shortComplexH1 A).moduleCatOpcyclesIso) fun f =>
        (cancel_epi (pOpcycles _)).1 <| ModuleCat.hom_ext <| Finsupp.lhom_ext fun a b => by
        have := congr($(chainsMap_f_1_comp_oneChainsIso (MonoidHom.id G) f) (single a b))
        simp_all [HomologicalComplex.opcyclesIsoSc', HomologicalComplex.opcyclesMap,
          shortComplexH1]

@[reassoc, elementwise]
theorem pOpcycles_comp_isoOneOpcyclesFunctor_hom_app :
    (inhomogeneousChains A).pOpcycles 1 â‰« (isoOneOpcyclesFunctor k G).hom.app A =
      (oneChainsIso _).hom â‰« (shortComplexH1 A).moduleCatRightHomologyData.p := by
  simp

/-- The functor sending a `G`-representation `A` to `Hâ‚(G, A)` is naturally isomorphic to the
general group homology functor at 1. -/
@[simps! hom_app inv_app]
noncomputable def isoH1Functor :
    functor k G 1 â‰… H1Functor k G :=
  NatIso.ofComponents (fun _ => isoH1 _) fun f => by simp

/-- The functor sending a `G`-representation `A` to `Zâ‚‚(G, A)` is naturally isomorphic to the
general cycles functor at 2. -/
@[simps! hom_app inv_app]
noncomputable def isoTwoCyclesFunctor :
    cyclesFunctor k G 2 â‰… twoCyclesFunctor k G :=
  NatIso.ofComponents (fun _ => isoTwoCycles _) fun f => by simp

/-- The functor sending a `G`-representation `A` to `Câ‚‚(G, A)/Bâ‚‚(G, A)` is naturally isomorphic to
the general opcocycles functor at 2. -/
@[simps! hom_app inv_app]
noncomputable def isoTwoOpcyclesFunctor :
    opcyclesFunctor k G 2 â‰… twoOpcyclesFunctor k G :=
  NatIso.ofComponents
    (fun A => (inhomogeneousChains A).opcyclesIsoSc' _ _ _ (by simp) (by simp) â‰ªâ‰« opcyclesMapIso
      (shortComplexH2Iso A) â‰ªâ‰« (shortComplexH2 A).moduleCatOpcyclesIso) fun f =>
        (cancel_epi (pOpcycles _)).1 <| ModuleCat.hom_ext <| Finsupp.lhom_ext fun a b => by
        have := congr($(chainsMap_f_2_comp_twoChainsIso (MonoidHom.id G) f) (single a b))
        simp_all [HomologicalComplex.opcyclesIsoSc', HomologicalComplex.opcyclesMap,
          shortComplexH2]

@[reassoc, elementwise]
theorem pOpcycles_comp_isoTwoOpcyclesFunctor_hom_app :
    (inhomogeneousChains A).pOpcycles 2 â‰« (isoTwoOpcyclesFunctor k G).hom.app A =
      (twoChainsIso _).hom â‰« (shortComplexH2 A).moduleCatRightHomologyData.p := by
  simp

/-- The functor sending a `G`-representation `A` to `Hâ‚‚(G, A)` is naturally isomorphic to the
general group homology functor at 2. -/
@[simps! hom_app inv_app]
noncomputable def isoH2Functor :
    functor k G 2 â‰… H2Functor k G :=
  NatIso.ofComponents (fun _ => isoH2 _) fun f => by simp

end NatIsos
end Functors
end groupHomology
