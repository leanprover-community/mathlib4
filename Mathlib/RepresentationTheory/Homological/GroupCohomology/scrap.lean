import Mathlib.RepresentationTheory.Homological.GroupCohomology.Functoriality
import Mathlib.RepresentationTheory.Homological.GroupHomology.Functoriality
import Mathlib.RepresentationTheory.Homological.GroupHomology.LongExactSequence

universe v u
noncomputable section

@[simp]
lemma QuotientGroup.mk'_comp_subtype {G : Type*} [Group G] (N : Subgroup G) [N.Normal] :
    (mk' N).comp N.subtype = 1 := by ext; simp

namespace CategoryTheory.ShortComplex

variable {R : Type u} [Ring R] (S : ShortComplex (ModuleCat.{v} R))

/-- Given a short complex `S` of modules, this is the isomorphism between the abstract `S.opcycles`
of the homology API and the more concrete description as `S.X‚ÇÇ ‚ß∏ LinearMap.range S.f.hom`. -/
def moduleCatOpcyclesIso : S.opcycles ‚âÖ ModuleCat.of R (S.X‚ÇÇ ‚ß∏ LinearMap.range S.f.hom) :=
  S.opcyclesIsoCokernel ‚â™‚â´ ModuleCat.cokernelIsoRangeQuotient _

@[reassoc (attr := simp), elementwise (attr := simp)]
theorem pOpcycles_comp_moduleCatOpcyclesIso_hom :
    S.pOpcycles ‚â´ S.moduleCatOpcyclesIso.hom = ModuleCat.ofHom (Submodule.mkQ _) := by
  simp [moduleCatOpcyclesIso]

@[reassoc (attr := simp), elementwise (attr := simp)]
theorem moduleCatOpcyclesIso_inv_comp_fromOpcycles :
    S.moduleCatOpcyclesIso.inv ‚â´ S.fromOpcycles = ModuleCat.ofHom (Submodule.liftQ
      (LinearMap.range S.f.hom) S.g.hom <|
      LinearMap.range_le_ker_iff.2 <| ModuleCat.hom_ext_iff.1 S.zero) := by
  have : Epi (ModuleCat.ofHom <| Submodule.mkQ (LinearMap.range S.f.hom)) :=
    (ModuleCat.epi_iff_surjective _).2 <| Submodule.Quotient.mk_surjective _
  simp only [‚Üê cancel_epi (ModuleCat.ofHom <| Submodule.mkQ <| LinearMap.range S.f.hom),
    moduleCatOpcyclesIso, Iso.trans_inv, ‚Üê Category.assoc]
  simp [‚Üê ModuleCat.ofHom_comp, Submodule.liftQ_mkQ]

theorem moduleCat_pOpcycles_eq_iff (x y : S.X‚ÇÇ) :
    S.pOpcycles x = S.pOpcycles y ‚Üî x - y ‚àà LinearMap.range S.f.hom :=
  Iff.trans ‚ü®fun h => by simpa using congr(S.moduleCatOpcyclesIso.hom $h),
    fun h => (ModuleCat.mono_iff_injective S.moduleCatOpcyclesIso.hom).1 inferInstance (by simpa)‚ü©
    (Submodule.Quotient.eq _)

end CategoryTheory.ShortComplex
namespace Representation

variable {k G V : Type*} [CommRing k] [Group G] [AddCommGroup V] [Module k V]
  (œÅ : Representation k G V) (S : Subgroup G) [S.Normal]

/-- Given a normal subgroup `S ‚â§ G`, a `G`-representation `œÅ` which is trivial on `S` factors
through `G ‚ß∏ S`. -/
noncomputable def ofQuotient [IsTrivial (œÅ.comp S.subtype)] :
    Representation k (G ‚ß∏ S) V :=
  (QuotientGroup.con S).lift œÅ <| by
    rintro x y ‚ü®‚ü®z, hz‚ü©, rfl‚ü©
    ext w
    have : œÅ y (œÅ z.unop _) = _ :=
      congr(œÅ y ($(IsTrivial.out (œÅ := œÅ.comp S.subtype) ‚ü®z.unop, hz‚ü©) w))
    simpa [‚Üê LinearMap.mul_apply, ‚Üê map_mul] using this

@[simp]
lemma ofQuotient_coe_apply [IsTrivial (œÅ.comp S.subtype)] (g : G) (x : V) :
    ofQuotient œÅ S (g : G ‚ß∏ S) x = œÅ g x :=
  rfl

/-- Given a `k`-linear `G`-representation `(V, œÅ)`, this is the representation defined by
restricting `œÅ` to a `G`-invariant `k`-submodule of `V`. -/
@[simps]
def subrepresentation (W : Submodule k V) (le_comap : ‚àÄ g, W ‚â§ W.comap (œÅ g)) :
    Representation k G W where
  toFun g := (œÅ g).restrict <| le_comap g
  map_one' := by ext; simp
  map_mul' _ _ := by ext; simp

/-- Given a `k`-linear `G`-representation `(V, œÅ)` and a `G`-invariant `k`-submodule `W ‚â§ V`, this
is the representation induced on `V ‚ß∏ W` by `œÅ`. -/
@[simps]
def quotient (W : Submodule k V) (le_comap : ‚àÄ g, W ‚â§ W.comap (œÅ g)) :
    Representation k G (V ‚ß∏ W) where
  toFun g := Submodule.mapQ _ _ (œÅ g) <| le_comap g
  map_one' := by ext; simp
  map_mul' _ _ := by ext; simp

section QuotientGroup

/-- Given a normal subgroup `S ‚â§ G`, a `G`-representation `œÅ` restricts to a `G`-representation on
the invariants of `œÅ|_S`. -/
noncomputable abbrev toInvariants :
    Representation k G (invariants (œÅ.comp S.subtype)) :=
  subrepresentation œÅ _ fun g x hx ‚ü®s, hs‚ü© => by
    simpa using congr(œÅ g $(hx ‚ü®(g‚Åª¬π * s * g), Subgroup.Normal.conj_mem' ‚Äπ_‚Ä∫ s hs g‚ü©))

instance : IsTrivial ((toInvariants œÅ S).comp S.subtype) where
  out g := LinearMap.ext fun ‚ü®x, hx‚ü© => Subtype.ext <| by simpa using (hx g)

/-- Given a normal subgroup `S ‚â§ G`, a `G`-representation `œÅ` induces a `G ‚ß∏ S`-representation on
the invariants of `œÅ|_S`. -/
noncomputable abbrev quotientToInvariants :
    Representation k (G ‚ß∏ S) (invariants (œÅ.comp S.subtype)) :=
  ofQuotient (toInvariants œÅ S) S

lemma le_comap_augmentationSubmodule (g : G) :
    augmentationSubmodule (œÅ.comp S.subtype) ‚â§
      (augmentationSubmodule <| œÅ.comp S.subtype).comap (œÅ g) :=
  Submodule.span_le.2 fun y ‚ü®‚ü®s, x‚ü©, hs‚ü© => by
    simpa [‚Üê hs] using mem_augmentationSubmodule_of_eq
      ‚ü®g * s * g‚Åª¬π, Subgroup.Normal.conj_mem ‚Äπ_‚Ä∫ s.1 s.2 g‚ü© (œÅ g x) _ <| by simp

/-- Given a normal subgroup `S ‚â§ G`, a `G`-representation `œÅ` restricts to a `G`-representation on
the augmentation submodule of `œÅ|_S`. -/
noncomputable abbrev toAugmentationSubmodule :=
  subrepresentation œÅ (augmentationSubmodule <| œÅ.comp S.subtype)
    fun g => le_comap_augmentationSubmodule œÅ S g

/-- Given a normal subgroup `S ‚â§ G`, a `G`-representation `œÅ` induces a `G`-representation on the
coinvariants of `œÅ|_S`. -/
noncomputable abbrev toCoinvariants :=
  quotient œÅ (augmentationSubmodule <| œÅ.comp S.subtype)
    fun g => le_comap_augmentationSubmodule œÅ S g

instance : IsTrivial ((toCoinvariants œÅ S).comp S.subtype) where
  out g := Submodule.linearMap_qext _ <| by
    ext x
    simpa [Submodule.Quotient.eq] using mem_augmentationSubmodule_of_eq g x _ rfl

/-- Given a normal subgroup `S ‚â§ G`, a `G`-representation `œÅ` induces a `G ‚ß∏ S`-representation on
the coinvariants of `œÅ|_S`. -/
noncomputable abbrev quotientToCoinvariants :
    Representation k (G ‚ß∏ S) (coinvariants (œÅ.comp S.subtype)) :=
  ofQuotient (toCoinvariants œÅ S) S

end QuotientGroup

end Representation

variable {k G : Type u} [CommRing k] [Group G] (A : Rep k G) (S : Subgroup G) [S.Normal]

open CategoryTheory
open Representation (IsTrivial)
namespace Rep

instance {H V : Type u} [Group H] [AddCommGroup V] [Module k V] (œÅ : Representation k H V)
    (f : G ‚Üí* H) [Representation.IsTrivial (œÅ.comp f)] :
    Representation.IsTrivial ((Rep.of œÅ).œÅ.comp f) := ‚Äπ_‚Ä∫

/-- Given a normal subgroup `S ‚â§ G`, a `G`-representation `œÅ` which is trivial on `S` factors
through `G ‚ß∏ S`. -/
noncomputable abbrev ofQuotient [Representation.IsTrivial (A.œÅ.comp S.subtype)] :
    Rep k (G ‚ß∏ S) := Rep.of (A.œÅ.ofQuotient S)

/-- Given a `k`-linear `G`-representation `(V, œÅ)`, this is the representation defined by
restricting `œÅ` to a `G`-invariant `k`-submodule of `V`. -/
noncomputable abbrev subrepresentation (W : Submodule k A) (le_comap : ‚àÄ g, W ‚â§ W.comap (A.œÅ g)) :
    Rep k G :=
  Rep.of (A.œÅ.subrepresentation W le_comap)

/-- The natural inclusion of a subrepresentation into the ambient representation. -/
@[simps]
def subtype (W : Submodule k A) (le_comap : ‚àÄ g, W ‚â§ W.comap (A.œÅ g)) :
    subrepresentation A W le_comap ‚ü∂ A where
  hom := ModuleCat.ofHom W.subtype
  comm _ := rfl

/-- Given a `k`-linear `G`-representation `(V, œÅ)` and a `G`-invariant `k`-submodule `W ‚â§ V`, this
is the representation induced on `V ‚ß∏ W` by `œÅ`.-/
noncomputable abbrev quotient (W) (le_comap) :=
  Rep.of (A.œÅ.quotient W le_comap)

/-- The natural projection from a representation to its quotient by a subrepresentation. -/
@[simps]
def mkQ (W : Submodule k A) (le_comap : ‚àÄ g, W ‚â§ W.comap (A.œÅ g)) :
    A ‚ü∂ quotient A W le_comap where
  hom := ModuleCat.ofHom <| Submodule.mkQ _
  comm _ := rfl

/-- Given a normal subgroup `S ‚â§ G`, a `G`-representation `œÅ` restricts to a `G`-representation on
the invariants of `œÅ|_S`. -/
noncomputable abbrev toInvariants :
    Rep k G := Rep.of <| A.œÅ.toInvariants S

/-- Given a normal subgroup `S ‚â§ G`, a `G`-representation `œÅ` induces a `G ‚ß∏ S`-representation on
the invariants of `œÅ|_S`. -/
abbrev quotientToInvariants (S : Subgroup G) [S.Normal] :=
  Rep.of (A.œÅ.quotientToInvariants S)

/-- Given a normal subgroup `S ‚â§ G`, a `G`-representation `œÅ` restricts to a `G`-representation on
the augmentation submodule of `œÅ|_S`. -/
abbrev toAugmentationSubmodule :=
  Rep.of (A.œÅ.toAugmentationSubmodule S)

/-- Given a normal subgroup `S ‚â§ G`, a `G`-representation `œÅ` induces a `G`-representation on the
coinvariants of `œÅ|_S`. -/
abbrev toCoinvariants :=
  Rep.of (A.œÅ.toCoinvariants S)

/-- Given a normal subgroup `S ‚â§ G`, a `G`-representation `A` induces a short exact sequence of
`G`-representations `0 ‚ü∂ ‚Ñê(S)A ‚ü∂ A ‚ü∂ A_S ‚ü∂ 0` where `‚Ñê(S)A` is the submodule of `A`
generated by elements of the form `œÅ(s)(x) - x` for `s : S, x : A`. -/
@[simps X‚ÇÅ X‚ÇÇ X‚ÇÉ f g]
def coinvariantsShortComplex : ShortComplex (Rep k G) where
  X‚ÇÅ := toAugmentationSubmodule A S
  X‚ÇÇ := A
  X‚ÇÉ := toCoinvariants A S
  f := subtype ..
  g := mkQ ..
  zero := by ext x; exact (Submodule.Quotient.mk_eq_zero _).2 x.2

lemma coinvariantsShortComplex_shortExact : (coinvariantsShortComplex A S).ShortExact where
  exact := (forget‚ÇÇ _ (ModuleCat k)).reflects_exact_of_faithful _ <|
    (ShortComplex.moduleCat_exact_iff _).2
      fun x hx => ‚ü®(‚ü®x, (Submodule.Quotient.mk_eq_zero _).1 hx‚ü© :
      Representation.augmentationSubmodule <| A.œÅ.comp S.subtype), rfl‚ü©
  mono_f := (Rep.mono_iff_injective _).2 fun _ _ h => Subtype.ext h
  epi_g := (Rep.epi_iff_surjective _).2 <| Submodule.mkQ_surjective _

/-- Given a normal subgroup `S ‚â§ G`, a `G`-representation `œÅ` induces a `G ‚ß∏ S`-representation on
the coinvariants of `œÅ|_S`. -/
abbrev quotientToCoinvariants :=
  ofQuotient (toCoinvariants A S) S

end Rep
namespace groupCohomology
open Rep ShortComplex

theorem congr {H : Type u} [Monoid H] {A : Rep k H} {B : Rep k G}
    {f‚ÇÅ f‚ÇÇ : G ‚Üí* H} (h : f‚ÇÅ = f‚ÇÇ) {œÜ : (Action.res _ f‚ÇÅ).obj A ‚ü∂ B} {T : Type*}
    (F : (f : G ‚Üí* H) ‚Üí (œÜ : (Action.res _ f).obj A ‚ü∂ B) ‚Üí T) :
    F f‚ÇÅ œÜ = F f‚ÇÇ (h ‚ñ∏ œÜ) := by
  subst h
  rfl

@[simp]
theorem mapOneCocycles_one {H : Type u} [Group H] {A : Rep k H} {B : Rep k G}
    (œÜ : (Action.res _ 1).obj A ‚ü∂ B) : mapOneCocycles 1 œÜ = 0 := by
  rw [mapOneCocycles, ‚Üê cancel_mono (moduleCatLeftHomologyData (shortComplexH1 B)).i,
    ShortComplex.cyclesMap'_i]
  refine ModuleCat.hom_ext (LinearMap.ext fun _ ‚Ü¶ funext fun y => ?_)
  simp [mapShortComplexH1, shortComplexH1, moduleCatMk, Pi.zero_apply y]

@[simp]
theorem H1Map_one {H : Type u} [Group H] {A : Rep k H} {B : Rep k G}
    (œÜ : (Action.res _ 1).obj A ‚ü∂ B) :
    H1Map 1 œÜ = 0 := by
  simp [‚Üê cancel_epi (H1œÄ _)]

/-- The short complex `H¬π(G ‚ß∏ S, A^S) ‚ü∂ H¬π(G, A) ‚ü∂ H¬π(S, A)`. -/
@[simps X‚ÇÅ X‚ÇÇ X‚ÇÉ f g]
def H1InfRes (A : Rep k G) (S : Subgroup G) [S.Normal] :
     ShortComplex (ModuleCat k) where
  X‚ÇÅ := H1 (A.quotientToInvariants S)
  X‚ÇÇ := H1 A
  X‚ÇÉ := H1 ((Action.res _ S.subtype).obj A)
  f := H1Map (QuotientGroup.mk' S) (subtype ..)
  g := H1Map S.subtype (ùüô _)
  zero := by
    rw [‚Üê H1Map_comp, Category.comp_id,
      congr (QuotientGroup.mk'_comp_subtype S) H1Map, H1Map_one]
    rintro g x hx ‚ü®s, hs‚ü©
    simpa using congr(A.œÅ g $(hx ‚ü®(g‚Åª¬π * s * g), Subgroup.Normal.conj_mem' ‚Äπ_‚Ä∫ s hs g‚ü©))

@[elab_as_elim]
theorem H1_induction_on {C : H1 A ‚Üí Prop}
    (h : ‚àÄ x : oneCocycles A, C (Submodule.Quotient.mk x)) (x : H1 A) :
    C x := Quotient.inductionOn' x h

@[simp]
lemma _root_.QuotientGroup.coe_subtype {G : Type*} [Group G] {S : Subgroup G} [S.Normal]
    (x : S) : (x : G ‚ß∏ S) = 1 := by simp

/-- The inflation map `H¬π(G ‚ß∏ S, A^S) ‚ü∂ H¬π(G, A)` is a monomorphism. -/
instance : Mono (H1InfRes A S).f := by
  rw [ModuleCat.mono_iff_injective, injective_iff_map_eq_zero]
  intro x hx
  induction' x using H1_induction_on with x
  simp_all only [H1InfRes_X‚ÇÇ, H1InfRes_X‚ÇÅ, H1InfRes_f, H1œÄ_comp_H1Map_apply (QuotientGroup.mk' S),
    Submodule.Quotient.mk_eq_zero]
  rcases hx with ‚ü®y, hy‚ü©
  refine ‚ü®‚ü®y, fun s => ?_‚ü©, Subtype.ext <| funext fun g => Quotient.inductionOn' g
    fun g => Subtype.ext <| congr_fun (Subtype.ext_iff.1 hy) g‚ü©
  simpa [sub_eq_zero, shortComplexH1, moduleCatToCycles] using congr_fun (Subtype.ext_iff.1 hy) s.1

/-- Given a `G`-representation `A` and a normal subgroup `S ‚â§ G`, the short complex
`H¬π(G ‚ß∏ S, A^S) ‚ü∂ H¬π(G, A) ‚ü∂ H¬π(S, A)` is exact. -/
instance : (H1InfRes A S).Exact := by
  rw [moduleCat_exact_iff_ker_sub_range]
  intro x hx
  induction' x using H1_induction_on with x
  simp_all only [H1InfRes_X‚ÇÇ, H1InfRes_X‚ÇÉ, H1InfRes_g, Submodule.Quotient.mk''_eq_mk, H1InfRes_X‚ÇÅ,
    LinearMap.mem_ker, H1œÄ_comp_H1Map_apply S.subtype, Submodule.Quotient.mk_eq_zero, H1InfRes_f]
  rcases hx with ‚ü®(y : A), hy‚ü©
  have h1 := (mem_oneCocycles_iff x).1 x.2
  have h2 : ‚àÄ s ‚àà S, x s = A.œÅ s y - y :=
    fun s hs => (groupCohomology.oneCocycles_ext_iff.1 hy ‚ü®s, hs‚ü©).symm
  refine ‚ü®H1œÄ _ ‚ü®fun g => Quotient.liftOn' g (fun g => ‚ü®x.1 g - A.œÅ g y + y, ?_‚ü©) ?_, ?_‚ü©, ?_‚ü©
  ¬∑ intro s
    calc
      _ = x (s * g) - x s - A.œÅ s (A.œÅ g y) + (x s + y) := by
        simp [add_eq_of_eq_sub (h2 s s.2), sub_eq_of_eq_add (h1 s g)]
      _ = x (g * (g‚Åª¬π * s * g)) - A.œÅ g (A.œÅ (g‚Åª¬π * s * g) y - y) - A.œÅ g y + y := by
        simp only [mul_assoc, mul_inv_cancel_left, map_mul, LinearMap.mul_apply, map_sub,
          Representation.œÅ_self_inv_apply]
        abel
      _ = x g - A.œÅ g y + y := by
        simp [eq_sub_of_add_eq' (h1 g (g‚Åª¬π * s * g)).symm,
          h2 (g‚Åª¬π * s * g) (Subgroup.Normal.conj_mem' ‚Äπ_‚Ä∫ _ s.2 _)]
  ¬∑ intro g h hgh
    have := congr(A.œÅ g $(h2 (g‚Åª¬π * h) <| QuotientGroup.leftRel_apply.1 hgh))
    simp_all [h1, ‚Üê sub_eq_add_neg, sub_eq_sub_iff_sub_eq_sub]
  ¬∑ rw [mem_oneCocycles_iff]
    intro g h
    induction' g using QuotientGroup.induction_on with g
    induction' h using QuotientGroup.induction_on with h
    apply Subtype.ext
    simp [‚Üê QuotientGroup.mk_mul, h1 g h, sub_add_eq_add_sub, add_assoc]
  ¬∑ symm
    simp only [ModuleCat.hom_ofHom, oneCocycles.val_eq_coe, Submodule.mkQ_apply,
      H1œÄ_comp_H1Map_apply, Submodule.Quotient.eq]
    use y
    refine Subtype.ext <| funext fun g => ?_
    simp only [moduleCatToCycles_apply_coe, AddSubgroupClass.coe_sub]
    simp [mapOneCocycles_comp_subtype_apply (A := A.quotientToInvariants S) (QuotientGroup.mk' S),
      shortComplexH1, oneCocycles.coe_mk (A := A.quotientToInvariants S), ‚Üê sub_sub]

end groupCohomology
namespace groupHomology
open Rep ShortComplex

noncomputable section
open Representation hiding leftRegular
open CategoryTheory groupHomology Rep Finsupp Limits Finset

variable [DecidableEq G]

section NotMap

theorem congr {H : Type u} [Monoid H] {A : Rep k G} {B : Rep k H}
    {f‚ÇÅ f‚ÇÇ : G ‚Üí* H} (h : f‚ÇÅ = f‚ÇÇ) {œÜ : A ‚ü∂ (Action.res _ f‚ÇÅ).obj B} {T : Type*}
    (F : (f : G ‚Üí* H) ‚Üí (œÜ : A ‚ü∂ (Action.res _ f).obj B) ‚Üí T) :
    F f‚ÇÅ œÜ = F f‚ÇÇ (h ‚ñ∏ œÜ) := by
  subst h
  rfl

lemma Representation.œÅ_eq_of_coe_eq {k G V : Type*} [CommRing k]
    [Group G] [AddCommGroup V] [Module k V] (œÅ : Representation k G V)
    (S : Subgroup G) [IsTrivial (œÅ.comp S.subtype)] (g h : G) (hgh : (g : G ‚ß∏ S) = h) :
    œÅ g = œÅ h := by
  ext x
  apply (œÅ.œÅ_apply_bijective g‚Åª¬π).1
  simpa [‚Üê LinearMap.mul_apply, ‚Üê map_mul, -isTrivial_def] using
    (congr($(isTrivial_def (œÅ.comp S.subtype) ‚ü®g‚Åª¬π * h, QuotientGroup.eq.1 hgh‚ü©) x)).symm

omit [DecidableEq G] [S.Normal] in
lemma œÅ_eq_of_coe_eq
    [IsTrivial (A.œÅ.comp S.subtype)] (g h : G) (hgh : (g : G ‚ß∏ S) = h) :
    A.œÅ g = A.œÅ h := by
  ext x
  apply (A.œÅ.œÅ_apply_bijective g‚Åª¬π).1
  simpa [‚Üê LinearMap.mul_apply, ‚Üê map_mul, -isTrivial_def] using
    (congr($(isTrivial_def (A.œÅ.comp S.subtype) ‚ü®g‚Åª¬π * h, QuotientGroup.eq.1 hgh‚ü©) x)).symm

/-- A `G`-representation `A` on which a normal subgroup `S ‚â§ G` acts trivially induces a
`G ‚ß∏ S`-representation on `A`, and composing this with the quotient map `G ‚Üí G ‚ß∏ S` gives the
original representation by definition. Useful for typechecking. -/
abbrev resOfQuotientIso [IsTrivial (A.œÅ.comp S.subtype)] :
    (Action.res _ (QuotientGroup.mk' S)).obj (A.ofQuotient S) ‚âÖ A := Iso.refl _

@[simp]
lemma H1Map_one {G H : Type u} [Group G] [Group H] [DecidableEq G] [DecidableEq H]
    {A : Rep k G} {B : Rep k H} (œÜ : A ‚ü∂ (Action.res _ (1 : G ‚Üí* H)).obj B) :
    H1Map (1 : G ‚Üí* H) œÜ = 0 := by
  simp only [‚Üê cancel_epi (H1œÄ A), H1œÄ_comp_H1Map, Limits.comp_zero]
  ext x
  rw [ModuleCat.hom_comp]
  refine (H1œÄ_eq_zero_iff _).2 ?_
  simpa [‚Üê mapDomain_mapRange] using
    Submodule.finsupp_sum_mem _ _ _ _ fun _ _ => single_one_mem_oneBoundaries _

/-- The short complex `H‚ÇÅ(S, A) ‚ü∂ H‚ÇÅ(G, A) ‚ü∂ H‚ÇÅ(G ‚ß∏ S, A_S)`. -/
@[simps X‚ÇÅ X‚ÇÇ X‚ÇÉ f g]
def H1CoresCoinf [DecidableEq (G ‚ß∏ S)] :
    ShortComplex (ModuleCat k) where
  X‚ÇÅ := H1 ((Action.res _ S.subtype).obj A)
  X‚ÇÇ := H1 A
  X‚ÇÉ := H1 (quotientToCoinvariants A S)
  f := H1Map S.subtype (ùüô _)
  g := H1Map (QuotientGroup.mk' S) (mkQ _ _ fun _ => le_comap_augmentationSubmodule _ _ _)
  zero := by rw [‚Üê H1Map_comp, congr (QuotientGroup.mk'_comp_subtype S) H1Map, H1Map_one]

/-- Given a `G`-representation `A` on which a normal subgroup `S ‚â§ G` acts trivially, this is the
short complex `H‚ÇÅ(S, A) ‚ü∂ H‚ÇÅ(G, A) ‚ü∂ H‚ÇÅ(G ‚ß∏ S, A)`. -/
@[simps X‚ÇÅ X‚ÇÇ X‚ÇÉ f g]
def H1CoresCoinfOfTrivial [DecidableEq (G ‚ß∏ S)] [IsTrivial (A.œÅ.comp S.subtype)] :
    ShortComplex (ModuleCat k) where
  X‚ÇÅ := H1 ((Action.res _ S.subtype).obj A)
  X‚ÇÇ := H1 A
  X‚ÇÉ := H1 (ofQuotient A S)
  f := H1Map S.subtype (ùüô _)
  g := H1Map (QuotientGroup.mk' S) <| (resOfQuotientIso A S).inv
  zero := by rw [‚Üê H1Map_comp, congr (QuotientGroup.mk'_comp_subtype S) H1Map, H1Map_one]

instance mapOneCycles_quotientGroupMk'_epi
    [DecidableEq (G ‚ß∏ S)] [IsTrivial (A.œÅ.comp S.subtype)] :
    Epi (mapOneCycles (QuotientGroup.mk' S) (resOfQuotientIso A S).inv) := by
  rw [ModuleCat.epi_iff_surjective]
  rintro ‚ü®x, hx‚ü©
  choose! s hs using QuotientGroup.mk_surjective (s := S)
  have hs‚ÇÅ : QuotientGroup.mk ‚àò s = id := funext hs
  refine ‚ü®‚ü®mapDomain s x, ?_‚ü©, Subtype.ext <| by
    rw [mapOneCycles_comp_subtype_apply]; simp [‚Üê mapDomain_comp, hs‚ÇÅ]‚ü©
  simpa [mem_oneCycles_iff, ‚Üê (mem_oneCycles_iff _).1 hx, sum_mapDomain_index_inj (f := s)
      (fun x y h => by rw [‚Üê hs x, ‚Üê hs y, h])]
    using Finsupp.sum_congr fun a b => QuotientGroup.induction_on a fun a => by
      simp [‚Üê QuotientGroup.mk_inv, œÅ_eq_of_coe_eq A S (s a)‚Åª¬π a‚Åª¬π (by simp [hs])]

instance H1Map_quotientGroupMk'_epi [DecidableEq (G ‚ß∏ S)] [IsTrivial (A.œÅ.comp S.subtype)] :
    Epi (H1Map (QuotientGroup.mk' S) (resOfQuotientIso A S).inv) := by
  convert epi_of_epi (H1œÄ A) _
  rw [H1œÄ_comp_H1Map]
  exact @epi_comp _ _ _ _ _ _ (mapOneCycles_quotientGroupMk'_epi A S) (H1œÄ _) inferInstance

/-- Given a `G`-representation `A` on which a normal subgroup `S ‚â§ G` acts trivially, the
induced map `H‚ÇÅ(G, A) ‚ü∂ H‚ÇÅ(G ‚ß∏ S, A)` is an epimorphism. -/
instance H1CoresCoinfOfTrivial_g_epi
    [DecidableEq (G ‚ß∏ S)] [IsTrivial (A.œÅ.comp S.subtype)] :
    Epi (H1CoresCoinfOfTrivial A S).g :=
  inferInstanceAs <| Epi (H1Map _ _)

/-- Given a `G`-representation `A` on which a normal subgroup `S ‚â§ G` acts trivially, the short
complex `H‚ÇÅ(S, A) ‚ü∂ H‚ÇÅ(G, A) ‚ü∂ H‚ÇÅ(G ‚ß∏ S, A)` is exact. -/
theorem H1CoresCoinfOfTrivial_exact [DecidableEq (G ‚ß∏ S)] [IsTrivial (A.œÅ.comp S.subtype)] :
    (H1CoresCoinfOfTrivial A S).Exact := by
  rw [ShortComplex.moduleCat_exact_iff_ker_sub_range]
  intro x hx
/- Denote `C(i) : C(S, A) ‚ü∂ C(G, A), C(œÄ) : C(G, A) ‚ü∂ C(G ‚ß∏ S, A)` and let `x : Z‚ÇÅ(G, A)` map to
0 in `H‚ÇÅ(G ‚ß∏ S, A)`. -/
  induction' x using Quotient.inductionOn' with x
  rcases x with ‚ü®(x : G ‚Üí‚ÇÄ A), (hxc : x ‚àà oneCycles A)‚ü©
  simp_all only [H1CoresCoinfOfTrivial_X‚ÇÇ, H1CoresCoinfOfTrivial_X‚ÇÉ, H1CoresCoinfOfTrivial_g,
    Submodule.Quotient.mk''_eq_mk, LinearMap.mem_ker, H1œÄ_comp_H1Map_apply (QuotientGroup.mk' S)]
/- Choose `y := ‚àë y(œÉ, œÑ)¬∑(œÉ, œÑ) ‚àà C‚ÇÇ(G ‚ß∏ S, A)` such that `C‚ÇÅ(œÄ)(x) = d(y)`. -/
  rcases (H1œÄ_eq_zero_iff _).1 hx with ‚ü®y, hy‚ü©
/- Let `s : G ‚ß∏ S ‚Üí G` be a section of the quotient map. -/
  choose! s hs using QuotientGroup.mk'_surjective S
  have hs‚ÇÅ : QuotientGroup.mk (s := S) ‚àò s = id := funext hs
  have hs‚ÇÇ : s.Injective := fun x y hxy => by rw [‚Üê hs x, ‚Üê hs y, hxy]
/- Let `z := ‚àë y(œÉ, œÑ)¬∑(s(œÉ), s(œÑ))`. -/
  let z : G √ó G ‚Üí‚ÇÄ A := lmapDomain _ k (Prod.map s s) y
/- We have that `C‚ÇÇ(œÄ)(z) = y`. -/
  have hz : lmapDomain _ k (QuotientGroup.mk' S) (dOne A z) = dOne (A.ofQuotient S) y := by
    have := congr($((mapShortComplexH1 (QuotientGroup.mk' S)
      (resOfQuotientIso A S).inv).comm‚ÇÅ‚ÇÇ.symm) z)
    simp_all [shortComplexH1, z, ‚Üê mapDomain_comp, Prod.map_comp_map]
  let v := x - dOne _ z
/- We have `C‚ÇÅ(s ‚àò œÄ)(v) = ‚àë v(g)¬∑s(œÄ(g)) = 0`, since `C‚ÇÅ(œÄ)(v) = dC‚ÇÅ(œÄ)(z) - C‚ÇÅ(œÄ)(dz) = 0` by
previous assumptions. -/
  have hv : mapDomain (s ‚àò QuotientGroup.mk) v = 0 := by
    rw [mapDomain_comp]
    simp_all [v, mapDomain, sum_sub_index]
  let e : G ‚Üí G √ó G := fun (g : G) => (s (g : G ‚ß∏ S), (s (g : G ‚ß∏ S))‚Åª¬π * g)
  have he : e.Injective := fun x y hxy => by
    obtain ‚ü®(h‚ÇÅ : s _ = s _), (h‚ÇÇ : _ * _ = _ * _)‚ü© := Prod.ext_iff.1 hxy
    exact (mul_right_inj _).1 (h‚ÇÅ ‚ñ∏ h‚ÇÇ)
/- Let `ve := ‚àë v(g)¬∑(s(œÄ(g)), s(œÄ(g))‚Åª¬πg)`. -/
  let ve : G √ó G ‚Üí‚ÇÄ A := mapDomain e v
  have hS : (v + dOne _ ve).support.toSet ‚äÜ S := by
  /- We have `d(ve) = ‚àë œÅ(s(œÄ(g))‚Åª¬π)(v(g))¬∑s(œÄ(g))‚Åª¬πg - ‚àë v(g)¬∑g + ‚àë v(g)¬∑s(œÄ(g))`.
    The second sum is `v`, so cancels: -/
    simp only [dOne, coe_lsum, he, ve, sum_mapDomain_index_inj, mul_inv_cancel_left,
      LinearMap.add_apply, LinearMap.sub_apply, LinearMap.coe_comp, Function.comp_apply,
      lsingle_apply, sum_add, sum_sub, sum_single, ‚Üê add_assoc, add_sub_cancel, e]
    intro w hw
    ¬∑ obtain (hl | hr) := Finset.mem_union.1 (support_add hw)
    /- The first sum clearly has support in `S`: -/
      ¬∑ obtain ‚ü®t, _, ht‚ü© := Finset.mem_biUnion.1 (support_sum hl)
        apply support_single_subset at ht
        simp_all [‚Üê QuotientGroup.eq, hs]
    /- The third sum is 0, by `hv`. -/
      ¬∑ simp_all [mapDomain]
  /- Now `v + d(ve)` has support in `S` and agrees with `x` in `H‚ÇÅ(G, A)`: -/
  use H1œÄ _ ‚ü®comapDomain Subtype.val (v + dOne _ ve) <|
    Set.injOn_of_injective Subtype.val_injective, ?_‚ü©
  ¬∑ simp only [H1CoresCoinfOfTrivial_X‚ÇÅ, H1CoresCoinfOfTrivial_f, Action.res_obj_V,
      ModuleCat.hom_ofHom, Submodule.mkQ_apply, H1œÄ_comp_H1Map_apply]
    refine (H1œÄ_eq_iff _ _).2 ?_
  /- Indeed, `v + d(ve) - x = d(ve - z) ‚àà B‚ÇÅ(G, A)`, since `v := x - dz`. -/
    use ve - z
    have := mapOneCycles_comp_subtype_apply (B := A) S.subtype (ùüô _)
    have := mapDomain_comapDomain (Œ± := S) Subtype.val Subtype.val_injective
      (v + dOne A ve) (fun x hx => ‚ü®‚ü®x, hS hx‚ü©, rfl‚ü©)
    simp_all [-mapOneCycles_comp_subtype_apply, v, add_sub_assoc, sub_add_sub_cancel']
  /- And `v + d(ve) := x - dz + d(ve)` is a 1-cycle because `x` is. -/
  ¬∑ have : v + dOne _ ve ‚àà oneCycles A := Submodule.add_mem _
      (Submodule.sub_mem _ hxc <| dOne_apply_mem_oneCycles _) (dOne_apply_mem_oneCycles _)
    rw [mem_oneCycles_iff] at this ‚ä¢
    rwa [‚Üê sum_comapDomain, ‚Üê sum_comapDomain (g := fun _ a => a)] at this <;>
    exact ‚ü®Set.mapsTo_preimage _ _, Set.injOn_of_injective Subtype.val_injective,
      fun x hx => ‚ü®‚ü®x, hS hx‚ü©, hx, rfl‚ü©‚ü©

lemma oneChainsToAugmentationSubmodule_surjective :
    Function.Surjective (oneChainsToAugmentationSubmodule A) := by
  rintro ‚ü®x, hx‚ü©
  rcases range_dZero_eq_augmentationSubmodule A ‚ñ∏ hx with ‚ü®y, hy‚ü©
  use y, Subtype.ext hy

/-- Given a `G`-representation `A` and a normal subgroup `S ‚â§ G`, let `I(S)A` denote the submodule
of `A` spanned by elements of the form `œÅ(s)(a) - a` for `s : S, a : A`. Then the image of
`C‚ÇÅ(G, I(S)A)` in `C‚ÇÅ(G, A)‚ß∏B‚ÇÅ(G, A)` is contained in the image of `C‚ÇÅ(S, A)`. -/
theorem comap_augmentationSubmodule_pOpcycles_range_subtype_pOpcycles_eq_top :
    Submodule.comap ((mapShortComplexH1 (MonoidHom.id G) (coinvariantsShortComplex A S).f).œÑ‚ÇÇ ‚â´
      (shortComplexH1 _).pOpcycles).hom (LinearMap.range ((mapShortComplexH1 S.subtype (ùüô _)).œÑ‚ÇÇ ‚â´
      (shortComplexH1 _).pOpcycles).hom) = ‚ä§ := by
  rw [eq_top_iff]
  intro x _
  rcases mapRange_surjective _ (map_zero _) (oneChainsToAugmentationSubmodule_surjective
    ((Action.res _ S.subtype).obj A)) x with ‚ü®(X : G ‚Üí‚ÇÄ S ‚Üí‚ÇÄ A), hX‚ü©
  let Y : S ‚Üí‚ÇÄ A := X.sum fun g f =>
    mapRange.linearMap (A.œÅ g‚Åª¬π) (lmapDomain _ k (fun s => MulAut.conjNormal g‚Åª¬π s) f) - f
  let Z : G √ó G ‚Üí‚ÇÄ A := X.sum fun g f =>
    lmapDomain _ k (fun s => (g, g‚Åª¬π * s.1 * g)) f - lmapDomain _ k (fun s => (s.1, g)) f
  use Y
  apply (moduleCat_pOpcycles_eq_iff _ _ _).2 ‚ü®Z, ?_‚ü©
  show dOne A Z = mapRange id rfl (lmapDomain _ k Subtype.val Y) -
    mapRange.linearMap (Submodule.subtype _) (mapDomain id x)
  simpa [map_finsupp_sum, mapDomain, map_sub, ‚Üê hX, sum_single_index, finsuppProdLEquiv,
    finsuppProdEquiv, Finsupp.uncurry, dOne, Y, Z, sum_mapRange_index,
    oneChainsToAugmentationSubmodule, dZero, single_sum, mul_assoc, sub_add_eq_add_sub,
    sum_sum_index, add_smul, sub_sub_sub_eq, lsingle, singleAddHom] using add_comm _ _

@[elab_as_elim]
theorem H1_induction_on {C : H1 A ‚Üí Prop}
    (h : ‚àÄ x : oneCycles A, C (Submodule.Quotient.mk x)) (x : H1 A) :
    C x := Quotient.inductionOn' x h

-- not sure why this is so slow even after I squeezed all the simps :(
set_option maxHeartbeats 320000 in
/-- Given a `G`-representation `A` and a normal subgroup `S ‚â§ G`, the short complex
`H‚ÇÅ(S, A) ‚ü∂ H‚ÇÅ(G, A) ‚ü∂ H‚ÇÅ(G ‚ß∏ S, A_S)` is exact. -/
instance [DecidableEq (G ‚ß∏ S)] :
    (H1CoresCoinf A S).Exact := by
  rw [ShortComplex.moduleCat_exact_iff_ker_sub_range]
  intro x hx
  induction' x using H1_induction_on with x
  simp only [H1CoresCoinf_X‚ÇÇ, H1CoresCoinf_X‚ÇÉ, LinearMap.mem_ker, H1CoresCoinf_g,
    H1œÄ_comp_H1Map_apply (QuotientGroup.mk' S)] at hx
/- Let `x : Z‚ÇÅ(G, A)` map to 0 in `H‚ÇÅ(G, ‚ß∏ S, A_S)`. Pick `y : C‚ÇÇ(G ‚ß∏ S, A_S)` such that `d(y)`
equals `Z‚ÇÅ(œÄ, œÄ)(x) : Z‚ÇÅ(G ‚ß∏ S, A_S)`. -/
  rcases (H1œÄ_eq_zero_iff _).1 hx with ‚ü®y, hy‚ü©
/- Then `Z‚ÇÅ(œÄ, Id)(x) : Z‚ÇÅ(G, A_S)` maps to 0 in `H‚ÇÅ(G ‚ß∏ S, A_S)`. We know
`H‚ÇÅ(S, A_S) ‚ü∂ H‚ÇÅ(G, A_S) ‚ü∂ H‚ÇÅ(G ‚ß∏ S, A_S)` is exact by `H1CoresCoinfOfTrivial_exact`, since
`S` acts trivially on `A_S`. So we can choose `z : Z‚ÇÅ(S, A_S)` with the same homology class as
`Z‚ÇÅ(œÄ, Id)(œÄ)` in `H‚ÇÅ(G, A_S)`. -/
  rcases @(ShortComplex.moduleCat_exact_iff_ker_sub_range _).1
    (H1CoresCoinfOfTrivial_exact (toCoinvariants A S) S)
    (H1œÄ _ <| mapOneCycles (MonoidHom.id G) (mkQ _ _ _) x) (by
      simpa only [H1CoresCoinfOfTrivial_X‚ÇÇ, H1CoresCoinfOfTrivial_X‚ÇÉ, H1CoresCoinfOfTrivial_g,
        Iso.refl_inv, ModuleCat.hom_ofHom, Submodule.mkQ_apply, LinearMap.mem_ker,
        H1œÄ_comp_H1Map_apply (QuotientGroup.mk' S), ‚Üê ConcreteCategory.comp_apply,
        ‚Üê cyclesMap'_comp, ‚Üê mapShortComplexH1_comp,
        congr (MonoidHom.comp_id _) mapShortComplexH1] using hx) with ‚ü®z, hz‚ü©
  induction' z using H1_induction_on with z
  simp only [H1CoresCoinfOfTrivial_X‚ÇÇ, H1CoresCoinfOfTrivial_X‚ÇÅ, H1CoresCoinfOfTrivial_f,
    H1œÄ_comp_H1Map_apply S.subtype, Action.res_obj_V, ModuleCat.hom_ofHom,
    Submodule.mkQ_apply] at hz
/- Choose `w : C‚ÇÇ(G, A_S)` such that `d(w) = Z‚ÇÅ(i, Id)(z) - Z‚ÇÅ(Id, œÄ)(x)`. -/
  rcases (H1œÄ_eq_iff _ _).1 hz with ‚ü®w, hzw‚ü©
/- Choose `Z : C‚ÇÅ(S, A)` mapping to `z : C‚ÇÅ(S, A_S)`, and `W : C‚ÇÇ(G, A)` mapping to
`w : C‚ÇÇ(G, A_S)`. -/
  rcases mapRange_surjective (coinvariantsMkQ _) (map_zero _)
    (Submodule.Quotient.mk_surjective _) z.1 with ‚ü®Z, hZ‚ü©
  rcases mapRange_surjective (coinvariantsMkQ _) (map_zero _)
    (Submodule.Quotient.mk_surjective _) w with ‚ü®W, hW‚ü©
/- Let `b : C‚ÇÅ(G, A)` denote `x + d(W) - C‚ÇÅ(i, Id)(z)`. -/
  let b : G ‚Üí‚ÇÄ A := (x.1 : G ‚Üí‚ÇÄ A) + dOne A W - lmapDomain _ k S.subtype Z
/- Then `b` has coefficients in `I(S)A := ‚ü®{œÅ(s)(a) - a | s ‚àà S, a ‚àà A}‚ü©`, since
`C‚ÇÅ(G, I(S)(A)) ‚ü∂ C‚ÇÅ(G, A) ‚ü∂ C‚ÇÅ(G, A_S)` is exact, and `b` is in the kernel of the second map. -/
  have hb : ‚àÄ g, b g ‚àà augmentationSubmodule (A.œÅ.comp S.subtype) :=
    fun g => (Submodule.Quotient.eq _).1 <| by
      show mapRange.linearMap (coinvariantsMkQ _) _ _ = mapRange.linearMap (coinvariantsMkQ _) _ _
      have := Finsupp.ext_iff.1 (congr($((mapShortComplexH1 (B := toCoinvariants A S)
        (MonoidHom.id G) (mkQ _ _ _)).comm‚ÇÅ‚ÇÇ.symm) W)) g
      simpa only [mapRange.linearMap_apply, mapRange_apply, Finsupp.coe_add, Pi.add_apply,
        Submodule.mkQ_apply, Submodule.Quotient.mk_add, Subgroup.coeSubtype, lmapDomain_apply,
        implies_true, ‚Üê mapDomain_mapRange, hZ, Action.res_obj_V, shortComplexH1,
        moduleCatMk_X‚ÇÅ_carrier, moduleCatMk_X‚ÇÇ_carrier, moduleCatMk_f, mapShortComplexH1_œÑ‚ÇÇ,
        ModuleCat.ofHom_comp, MonoidHom.coe_id, lmapDomain_id, ModuleCat.ofHom_id, mkQ_hom,
        ModuleCat.hom_ofHom, Category.id_comp, mapShortComplexH1_œÑ‚ÇÅ, Prod.map_id,
        ModuleCat.hom_comp, LinearMap.coe_comp, Function.comp_apply, hW, hzw,
        mapOneCycles_comp_subtype_apply (B := toCoinvariants A S), mapDomain_id, Finsupp.coe_sub,
        Pi.sub_apply, eq_sub_iff_add_eq'] using this
/- Let `Œ≤` be `b` considered as an element of `C‚ÇÅ(G, I(S)(A))`, so that `C‚ÇÅ(Id, i)(Œ≤) = b`. -/
  let Œ≤ : G ‚Üí‚ÇÄ augmentationSubmodule (A.œÅ.comp S.subtype) :=
    mapRange (Function.invFun <| (augmentationSubmodule (A.œÅ.comp S.subtype)).subtype)
    (Function.leftInverse_invFun Subtype.val_injective (0 : augmentationSubmodule _)) b
  have hŒ≤b : mapRange Subtype.val rfl Œ≤ = b := Finsupp.ext fun g => Subtype.ext_iff.1 <|
    Function.leftInverse_invFun Subtype.val_injective ‚ü®b g, hb g‚ü©
/- Then, since the image of `C‚ÇÅ(G, I(S)A)` in `C‚ÇÅ(G, A)‚ß∏B‚ÇÅ(G, A)` is contained in the image of
`C‚ÇÅ(S, A)` by `comap_augmentationSubmodule_pOpcycles_range_subtype_pOpcycles_eq_top`, we can choose
`Œ± : C‚ÇÅ(S, A)`, `Œ¥ : C‚ÇÇ(G, A)` such that `d(Œ¥) = Z‚ÇÅ(i, Id)(Œ±) - Z‚ÇÅ(Id, i)(Œ≤)`. -/
  rcases eq_top_iff.1 (comap_augmentationSubmodule_pOpcycles_range_subtype_pOpcycles_eq_top A S)
    (by trivial : Œ≤ ‚àà ‚ä§) with ‚ü®(Œ± : S ‚Üí‚ÇÄ A), hŒ±‚ü©
  dsimp only [ModuleCat.hom_comp] at hŒ±
  rcases (moduleCat_pOpcycles_eq_iff _ _ _).1 hŒ± with ‚ü®(Œ¥ : G √ó G ‚Üí‚ÇÄ A), hŒ≤‚ü©
/- Then, by assumption, `d(W + Œ¥) = C‚ÇÅ(i, Id)(Œ± + Z) - x`. -/
  have hŒ±Z : dOne A (W + Œ¥) = mapDomain Subtype.val (Œ± + Z) - x := by
    simp_all only [shortComplexH1, moduleCatMk_X‚ÇÇ_carrier, moduleCatMk_X‚ÇÉ_carrier,
      moduleCatMk_g, ModuleCat.hom_ofHom, moduleCatMk_X‚ÇÅ_carrier, Submodule.Quotient.mk_eq_zero,
      LinearMap.mem_range, Action.res_obj_V, Subgroup.coeSubtype, lmapDomain_apply, Finsupp.coe_sub,
      Finsupp.coe_add, Pi.sub_apply, Pi.add_apply, mapShortComplexH1_œÑ‚ÇÇ, ModuleCat.ofHom_comp,
      Action.id_hom, ModuleCat.hom_id, mapRange.linearMap_id, ModuleCat.ofHom_id, Category.comp_id,
      LinearMap.coe_comp, Function.comp_apply, coinvariantsShortComplex_X‚ÇÅ, Submodule.coe_subtype,
      coinvariantsShortComplex_f, MonoidHom.coe_id, lmapDomain_id, subtype_hom, Category.id_comp,
      mapRange.linearMap_apply, map_sub, map_add, moduleCatMk_f, ‚Üê sub_add, ‚Üê sub_sub,
      sub_add_eq_add_sub, add_sub_cancel, mapDomain_add, b]
/- So we claim that `Œ± + Z` is an element of `Z‚ÇÅ(S, A)` which differs from `x` by a boundary in
`Z‚ÇÅ(G, A)`. -/
  use H1œÄ _ ‚ü®Œ± + Z, ?_‚ü©
/- Indeed, by `hŒ±Z`, `d(W + Œ¥)` is the desired boundary: -/
  ¬∑ simp only [H1CoresCoinf_X‚ÇÇ, H1CoresCoinf_X‚ÇÅ, Submodule.mkQ_apply, H1CoresCoinf_f,
      ModuleCat.hom_ofHom, H1œÄ_comp_H1Map_apply, b]
    refine (H1œÄ_eq_iff _ _).2 ‚ü®W + Œ¥, ?_‚ü©
    have := mapOneCycles_comp_subtype_apply (B := A) S.subtype (ùüô _)
    simp_all only [Submodule.Quotient.mk_eq_zero, LinearMap.mem_range, Action.res_obj_V,
      mapShortComplexH1_œÑ‚ÇÇ, ModuleCat.ofHom_comp, Subgroup.coeSubtype, Action.id_hom,
      ModuleCat.hom_id, mapRange.linearMap_id, ModuleCat.ofHom_id, Category.comp_id,
      ModuleCat.hom_ofHom, LinearMap.coe_comp, Function.comp_apply, coinvariantsShortComplex_f,
      coinvariantsShortComplex_X‚ÇÅ, MonoidHom.coe_id, lmapDomain_id, subtype_hom, Category.id_comp,
      map_add, LinearMap.id_coe, mapRange_id, b]
/- And `Œ± + Z` is a cycle, since `d(W + Œ¥) + x` is. -/
  ¬∑ rw [mem_oneCycles_iff]
    have : x + dOne A (W + Œ¥) ‚àà oneCycles A := Submodule.add_mem _ x.2 (dOne_apply_mem_oneCycles _)
    rwa [eq_sub_iff_add_eq'.1 hŒ±Z, mem_oneCycles_iff, sum_mapDomain_index_inj
      Subtype.val_injective, sum_mapDomain_index_inj Subtype.val_injective] at this

@[simp]
lemma _root_.Rep.res_obj_œÅ {H : Type u} [Monoid H] (f : G ‚Üí* H) (A : Rep k H) (g : G) :
    DFunLike.coe (F := G ‚Üí* (A ‚Üí‚Çó[k] A)) (œÅ ((Action.res _ f).obj A)) g = A.œÅ (f g) := rfl

/-- Given a `G`-representation `A` and a normal subgroup `S ‚â§ G`, the map
`H‚ÇÅ(G, A) ‚ü∂ H‚ÇÅ(G ‚ß∏ S, A_S)` is an epimorphism. -/
instance [DecidableEq (G ‚ß∏ S)] :
    Epi (H1CoresCoinf A S).g := by
  rw [ModuleCat.epi_iff_surjective]
  intro x
  induction' x using H1_induction_on with x
/- Let `x : Z‚ÇÅ(G ‚ß∏ S, A_S)`. We know `Z‚ÇÅ(G, A_S) ‚ü∂ Z‚ÇÅ(G ‚ß∏ S, A_S)` is surjective, so pick
`y : Z‚ÇÅ(G, A_S)` in the preimage of `x`. -/
  rcases (ModuleCat.epi_iff_surjective _).1
    (mapOneCycles_quotientGroupMk'_epi (A.toCoinvariants S) S) x with ‚ü®y, hy‚ü©
/- We know `C‚ÇÅ(G, A) ‚ü∂ C‚ÇÅ(G, A_S)` is surjective, so pick `Y` in the preimage of `y`. -/
  rcases mapRange_surjective _ (map_zero _) (Submodule.mkQ_surjective
    (augmentationSubmodule (A.œÅ.comp S.subtype))) y.1 with ‚ü®Y, hY‚ü©
/- Then `d(Y) ‚àà I(S)A,` since `d(y) = 0`. -/
  have : dZero _ Y ‚àà augmentationSubmodule (A.œÅ.comp S.subtype) := by
    have h' := congr($((mapShortComplexH1 (B := toCoinvariants A S)
      (MonoidHom.id G) (mkQ _ _ _)).comm‚ÇÇ‚ÇÉ) Y)
    simp_all [shortComplexH1, ‚Üê Submodule.Quotient.mk_eq_zero]
  /- Thus we can pick a representation of `d(Y)` as a sum `‚àë œÅ(s·µ¢‚Åª¬π)(a·µ¢) - a·µ¢`, `s·µ¢ ‚àà S, a·µ¢ ‚àà A`,
and `Y - ‚àë a·µ¢¬∑s·µ¢` is a cycle. -/
  rcases oneChainsToAugmentationSubmodule_surjective
    ((Action.res _ S.subtype).obj A) ‚ü®dZero A Y, this‚ü© with ‚ü®(Z : S ‚Üí‚ÇÄ A), hZ‚ü©
  have H : dZero A (Y - mapDomain S.subtype Z) = 0 := by
    simpa [map_sub, sub_eq_zero, oneChainsToAugmentationSubmodule, - LinearMap.sub_apply, dZero,
      sum_mapDomain_index_inj] using Subtype.ext_iff.1 hZ.symm
  use H1œÄ A ‚ü®Y - mapDomain S.subtype Z, H‚ü©
  simp only [H1CoresCoinf_X‚ÇÉ, H1CoresCoinf_X‚ÇÇ, H1CoresCoinf_g, ModuleCat.hom_ofHom,
    Subgroup.coeSubtype, Submodule.mkQ_apply, H1œÄ_comp_H1Map_apply]
/- Moreover, the image of `Y - ‚àë a·µ¢¬∑s·µ¢` in `Z‚ÇÅ(G ‚ß∏ S, A_S)` is `x - ‚àë a·µ¢¬∑1`, and hence differs from
`x` by a boundary, since `a·µ¢¬∑1 = d(a·µ¢¬∑(1, 1))`. -/
  refine (H1œÄ_eq_iff _ _).2 ?_
  rw [‚Üê hy, mapOneCycles_comp_subtype_apply, mapOneCycles_comp_subtype_apply,
    ‚Üê lmapDomain_apply _ k]
  simpa [map_sub, mapRange_sub, hY, ‚Üê mapDomain_comp, ‚Üê mapDomain_mapRange, Function.comp_def]
      using Submodule.finsupp_sum_mem _ _ _ _ fun _ _ => single_one_mem_oneBoundaries _

end NotMap
end
end groupHomology
