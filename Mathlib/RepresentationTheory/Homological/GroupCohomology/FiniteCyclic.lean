/-
Copyright (c) 2025 Amelia Livingston. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Amelia Livingston
-/
import Mathlib.RepresentationTheory.Homological.FiniteCyclic
import Mathlib.RepresentationTheory.Homological.GroupCohomology.LowDegree

/-!
# Group cohomology of a finite cyclic group

Let `k` be a commutative ring, `G` a group and `A` a `k`-linear `G`-representation. Given
endomorphisms `Ï†, Ïˆ : A âŸ¶ A` such that `Ï† âˆ˜ Ïˆ = Ïˆ âˆ˜ Ï† = 0`. Denote by `Chains(A, Ï†, Ïˆ)` the
periodic chain complex `... âŸ¶ A --Ï†--> A --Ïˆ--> A --Ï†--> A --Ïˆ--> A âŸ¶ 0` and by
`Cochains(A, Ï†, Ïˆ)` the periodic cochain complex
`0 âŸ¶ A --Ïˆ--> A --Ï†--> A --Ïˆ--> A --Ï†--> A âŸ¶ ...`.

When `G` is finite and generated by `g : G`, then `P := Chains(k[G], N, Ï(g) - Id)` (with `Ï` the
left regular representation) is a projective resolution of `k` as a trivial representation.
In this file we show that for `A : Rep k G`, `Hom(P, A)` is isomorphic to
`Cochains(A, N, Ï_A(g) - Id)` as a complex of `k`-modules, and hence the cohomology of this complex
computes group cohomology.

## Main definitions

* `Rep.FiniteCyclicGroup.groupCohomologyIsoâ‚€ A g hg`: given a finite cyclic group `G` generated by
  `g`, and a representation `A : Rep k G`, this is an isomorphism `Hâ°(G, A) â‰… Ker(Ï_A(g) - Id)`.
* `Rep.FiniteCyclicGroup.groupCohomologyIsoOdd A g hg i hi`: given a finite cyclic group `G`
  generated by `g`, and a representation `A : Rep k G`, this is an isomorphism between `Hâ±(G, A)`
  and the homology of `A --(Ï(g) - Id)--> A --N--> A` for all odd `i`.
* `Rep.FiniteCyclicGroup.groupCohomologyIsoEven A g hg i hi`: given a finite cyclic group `G`
  generated by `g`, and a representation `A : Rep k G`, this is an isomorphism between `Hâ±(G, A)`
  and the homology of `A --N--> A --(Ï(g) - Id)--> A` for all positive even `i`.

-/

universe v u

open CategoryTheory

namespace Rep.FiniteCyclicGroup

variable {k G : Type u} [CommRing k] [CommGroup G] [Fintype G] (A : Rep k G) (g : G)
  (hg : âˆ€ x, x âˆˆ Subgroup.zpowers g)

/-- Given a finite cyclic group `G` generated by `g : G` and a `k`-linear `G`-representation `A`,
the periodic cochain complex
`0 âŸ¶ Hom(k[G], A) --(- âˆ˜ (Ï(g) - ğŸ™))--> Hom(k[G], A) --(- âˆ˜ N)--> Hom(k[G], A) âŸ¶ ...`
is isomorphic as a complex in `ModuleCat k` to
`0 âŸ¶ A --(Ï(g) - ğŸ™)--> A --N--> A --(Ï(g) - ğŸ™)--> A --N--> A âŸ¶ ...`. -/
@[simps!]
noncomputable def homResolutionIso :
    (resolution k g hg).complex.linearYonedaObj k A â‰… moduleCatCochainComplex A g :=
  HomologicalComplex.Hom.isoOfComponents (fun _ => (leftRegularHomEquiv A).toModuleIso) <| by
    rintro i j âŸ¨rflâŸ©
    ext (x : leftRegular _ _ âŸ¶ _)
    by_cases hi : Even i
    Â· have : Â¬(Even (i + 1)) := (not_iff_comm.1 Nat.even_add_one.symm).2 hi
      simp [hi, this, â† hom_comm_apply x]
    Â· simp [hi, Nat.even_add_one.2 hi, Representation.norm, â† hom_comm_apply x]

open ShortComplex Limits

/-- Given a finite cyclic group `G` generated by `g` and `A : Rep k G`, `Hâ°(G, A)` is isomorphic
to the kernel of `Ï(g) - Id(A)`. -/
noncomputable abbrev groupCohomologyIsoâ‚€ (hg : âˆ€ x, x âˆˆ Subgroup.zpowers g) :
    groupCohomology A 0 â‰… ModuleCat.of k (LinearMap.ker (applyAsHom A g - ğŸ™ A).hom.hom) :=
  groupCohomology.H0Iso A â‰ªâ‰« (LinearEquiv.ofEq _ _ <| by ext; simpa [sub_eq_zero]
    using Representation.FiniteCyclicGroup.mem_invariants_iff A.Ï g hg _).toModuleIso

variable [DecidableEq G]

/-- Given a finite cyclic group `G` generated by `g` and `A : Rep k G`, `Hâ±(G, A)` is isomorphic
to the homology of the short complex of `k`-modules `A --N--> A --(Ï(g) - ğŸ™)--> A` when `i` is
nonzero and even. -/
noncomputable def groupCohomologyIsoEven
    (hg : âˆ€ x, x âˆˆ Subgroup.zpowers g) (i : â„•) [hâ‚€ : NeZero i] (hi : Even i) :
    groupCohomology A i â‰… (normHomCompSub A g).homology :=
  groupCohomologyIso A i (resolution k g hg) â‰ªâ‰«
  (HomologicalComplex.homologyMapIso (homResolutionIso A g hg) i) â‰ªâ‰«
  HomologicalComplex.alternatingConstHomologyIsoEven A.V (by ext; simp) (by ext; simp) (by simp)
    (by induction i generalizing hâ‚€ with | zero => exact (NeZero.ne 0 rfl).elim | succ n _ => simp)
    (by simp) hi

/-- Given a finite cyclic group `G` generated by `g` and `A : Rep k G`, this is the quotient map
`Ker(Ï(g) - Id(A)) âŸ¶ Ker(Ï(g) - Id(A))/Im(N) â‰… Hâ±(G, A)` for any nonzero even `i`. -/
noncomputable abbrev groupCohomologyÏ€Even
    (hg : âˆ€ x, x âˆˆ Subgroup.zpowers g) (i : â„•) [NeZero i] (hi : Even i) :
    ModuleCat.of k (LinearMap.ker (applyAsHom A g - ğŸ™ A).hom.hom) âŸ¶ groupCohomology A i :=
  (moduleCatCyclesIso <| normHomCompSub A g).inv â‰«
      ShortComplex.homologyÏ€ _ â‰« (groupCohomologyIsoEven A g hg i hi).inv

lemma groupCohomologyÏ€Even_eq_zero_iff
    (hg : âˆ€ x, x âˆˆ Subgroup.zpowers g) (i : â„•) [NeZero i] (hi : Even i)
    (x : LinearMap.ker (applyAsHom A g - ğŸ™ A).hom.hom) :
    groupCohomologyÏ€Even A g hg i hi x = 0 â†” x.1 âˆˆ LinearMap.range A.norm.hom.hom := by
  simp [groupCohomologyÏ€Even, map_eq_zero_iff _ ((ModuleCat.mono_iff_injective _).1 inferInstance),
    moduleCatToCycles, -LinearMap.mem_range, LinearMap.range_codRestrict]

lemma groupCohomologyÏ€Even_eq_iff
    (hg : âˆ€ x, x âˆˆ Subgroup.zpowers g) (i : â„•) [NeZero i] (hi : Even i)
    (x y : LinearMap.ker (applyAsHom A g - ğŸ™ A).hom.hom) :
    groupCohomologyÏ€Even A g hg i hi x = groupCohomologyÏ€Even A g hg i hi y â†”
      x.1 - y.1 âˆˆ LinearMap.range A.norm.hom.hom := by
  rw [â† sub_eq_zero, â† map_sub, groupCohomologyÏ€Even_eq_zero_iff]; rfl

/-- Given a finite cyclic group `G` generated by `g` and `A : Rep k G`, `Hâ±(G, A)` is isomorphic
to the homology of the short complex of `k`-modules `A --(Ï(g) - ğŸ™)--> A --N--> A` when `i` is
odd. -/
noncomputable def groupCohomologyIsoOdd (hg : âˆ€ x, x âˆˆ Subgroup.zpowers g) (i : â„•) (hi : Odd i) :
    groupCohomology A i â‰… (subCompNormHom A g).homology :=
  groupCohomologyIso A i (resolution k g hg) â‰ªâ‰«
  (HomologicalComplex.homologyMapIso (homResolutionIso A g hg) i) â‰ªâ‰«
  HomologicalComplex.alternatingConstHomologyIsoOdd A.V (by ext; simp) (by ext; simp)
    (by simp) (by rcases hi with âŸ¨j, rflâŸ©; simp) (by simp) hi

/-- Given a finite cyclic group `G` generated by `g` and `A : Rep k G`, this is the quotient map
`Ker(N) âŸ¶ Ker(N)/Im(Ï(g) - Id(A)) â‰… Hâ±(G, A)` for any odd `i`. -/
noncomputable abbrev groupCohomologyÏ€Odd (hg : âˆ€ x, x âˆˆ Subgroup.zpowers g) (i : â„•) (hi : Odd i) :
    ModuleCat.of k (LinearMap.ker A.norm.hom.hom) âŸ¶ groupCohomology A i :=
    (moduleCatCyclesIso <| subCompNormHom A g).inv â‰«
      ShortComplex.homologyÏ€ _ â‰« (groupCohomologyIsoOdd A g hg i hi).inv

lemma groupCohomologyÏ€Odd_eq_zero_iff (hg : âˆ€ x, x âˆˆ Subgroup.zpowers g) (i : â„•) (hi : Odd i)
    (x : LinearMap.ker A.norm.hom.hom) :
    groupCohomologyÏ€Odd A g hg i hi x = 0 â†”
      x.1 âˆˆ LinearMap.range (applyAsHom A g - ğŸ™ A).hom.hom := by
  simp [groupCohomologyÏ€Odd, map_eq_zero_iff _ ((ModuleCat.mono_iff_injective _).1 inferInstance),
    moduleCatToCycles, -LinearMap.mem_range, LinearMap.range_codRestrict]

lemma groupCohomologyÏ€Odd_eq_iff (hg : âˆ€ x, x âˆˆ Subgroup.zpowers g) (i : â„•) (hi : Odd i)
    (x y : LinearMap.ker A.norm.hom.hom) :
    groupCohomologyÏ€Odd A g hg i hi x = groupCohomologyÏ€Odd A g hg i hi y â†”
      x.1 - y.1 âˆˆ LinearMap.range (applyAsHom A g - ğŸ™ A).hom.hom := by
  rw [â† sub_eq_zero, â† map_sub, groupCohomologyÏ€Odd_eq_zero_iff]; rfl

end Rep.FiniteCyclicGroup
