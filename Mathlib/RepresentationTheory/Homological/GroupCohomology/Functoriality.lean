import Mathlib.Algebra.Homology.ConcreteCategory
import Mathlib.Algebra.Homology.HomologicalComplexAbelian
import Mathlib.Algebra.Homology.ShortComplex.ShortExact
import Mathlib.RepresentationTheory.Homological.GroupCohomology.Basic
import Mathlib.RepresentationTheory.Invariants
import Mathlib.RepresentationTheory.Homological.GroupCohomology.LowDegree
universe v u
variable (n : â„•)

open CategoryTheory

lemma Fin.comp_contractNth {G H : Type*} [MulOneClass G] [MulOneClass H] (f : G â†’* H)
    (j : Fin (n + 1)) (g : Fin (n + 1) â†’ G) :
    f âˆ˜ Fin.contractNth j (Â· * Â·) g = Fin.contractNth j (Â· * Â·) (f âˆ˜ g) := by
  ext x
  rcases lt_trichotomy (x : â„•) j with (h|h|h)
  Â· simp only [Function.comp_apply, Fin.contractNth_apply_of_lt, h]
  Â· simp only [Function.comp_apply, Fin.contractNth_apply_of_eq, h, f.map_mul]
  Â· simp only [Function.comp_apply, Fin.contractNth_apply_of_gt, h]

namespace LinearMap

lemma ker_compLeft
    {R M N : Type*} [Semiring R] [AddCommMonoid M] [AddCommMonoid N]
    [Module R M] [Module R N] (f : M â†’â‚—[R] N) (I : Type*) :
    LinearMap.ker (f.compLeft I) = Submodule.pi (Set.univ : Set I) (fun _ => LinearMap.ker f) :=
  Submodule.ext fun _ => âŸ¨fun (hx : _ = _) i _ => congr_fun hx i,
    fun hx => funext fun i => hx i trivialâŸ©

lemma range_compLeft
    {R M N : Type*} [Semiring R] [AddCommMonoid M] [AddCommMonoid N]
    [Module R M] [Module R N] (f : M â†’â‚—[R] N) (I : Type*) :
    LinearMap.range (f.compLeft I) = Submodule.pi (Set.univ : Set I) (fun _ => LinearMap.range f) :=
  Submodule.ext fun _ => âŸ¨fun âŸ¨y, hyâŸ© i _ => âŸ¨y i, congr_fun hy iâŸ©, fun hx => by
    choose y hy using hx
    exact âŸ¨fun i => y i trivial, funext fun i => hy i trivialâŸ©âŸ©

end LinearMap
namespace ModuleCat

variable (R : Type u) [Ring R]

lemma ofHom_comp {M N P : Type v} [AddCommGroup M] [AddCommGroup N] [AddCommGroup P]
    [Module R M] [Module R N] [Module R P] (f : M â†’â‚—[R] N) (g : N â†’â‚—[R] P) :
    ofHom (g âˆ˜â‚— f) = ofHom f â‰« ofHom g := rfl

end ModuleCat

namespace groupCohomology
open Rep

variable {k G H : Type u} [CommRing k] [Group G] [Group H]
  (A : Rep k H) (B : Rep k G) (f : G â†’* H) (Ï† : A â†’â‚—[k] B) (n : â„•)

class IsPairMap : Prop where
  compatible : âˆ€ (g : G), Ï† âˆ˜â‚— A.Ï (f g) = B.Ï g âˆ˜â‚— Ï†

namespace IsPairMap
open Representation

variable {A B f Ï†} (S : Subgroup G)

lemma compatible_apply [IsPairMap A B f Ï†] (g : G) (x : A) :
    Ï† (A.Ï (f g) x) = B.Ï g (Ï† x) :=
  congr($(compatible g) x)

instance inst_comp {k G H K : Type u} [CommRing k] [Group G] [Group H] [Group K]
    (A : Rep k K) (B : Rep k H) (C : Rep k G) (f : H â†’* K) (g : G â†’* H) (Ï† : A â†’â‚—[k] B)
    (Ïˆ : B â†’â‚—[k] C) [IsPairMap A B f Ï†] [IsPairMap B C g Ïˆ] :
    IsPairMap A C (f.comp g) (Ïˆ.comp Ï†) where
  compatible x := by
    ext y
    have := congr($(compatible (A := A) (B := B) (f := f) (Ï† := Ï†) (g x)) y)
    have := congr($(compatible (A := B) (B := C) (f := g) (Ï† := Ïˆ) x) (Ï† y))
    simp_all

instance inst_comp_left {k G H : Type u} [CommRing k] [Group G] [Group H] (A : Rep k G)
    (B C : Rep k H) (f : H â†’* G) (Ï† : A â†’â‚—[k] B) (Ïˆ : B â†’â‚—[k] C)
    [IsPairMap A B f Ï†] [IsPairMap B C (MonoidHom.id H) Ïˆ] :
    IsPairMap A C f (Ïˆ.comp Ï†) :=
  inst_comp A B C f (MonoidHom.id _) Ï† Ïˆ

instance inst_comp_right {k G H : Type u} [CommRing k] [Group G] [Group H] (A B : Rep k G)
    (C : Rep k H) (f : H â†’* G) (Ï† : A â†’â‚—[k] B) (Ïˆ : B â†’â‚—[k] C)
    [IsPairMap A B (MonoidHom.id G) Ï†] [IsPairMap B C f Ïˆ] :
    IsPairMap A C f (Ïˆ.comp Ï†) :=
  inst_comp A B C (MonoidHom.id _) f Ï† Ïˆ

instance inst_inf [S.Normal] : IsPairMap (Rep.inf B S) B (QuotientGroup.mk' S)
    (invariants (B.Ï.comp S.subtype)).subtype where
  compatible := by intros; rfl

instance inst_res : IsPairMap A ((Action.res _ f).obj A) f LinearMap.id where
  compatible := by intros; rfl

instance inst_hom {A B : Rep k G} (f : A âŸ¶ B) : IsPairMap A B (MonoidHom.id G) f.hom where
  compatible := f.comm

instance inst_hom' {A B : Rep k G} (f : A âŸ¶ B) : IsPairMap A B (MonoidHom.id G) (hom f) where
  compatible := f.comm

instance inst_comp_hom {A B C : Rep k G} (f : A âŸ¶ B) (g : B âŸ¶ C) :
    IsPairMap A C (MonoidHom.id G) (f.hom â‰« g.hom) :=
  inst_hom' (f â‰« g)

instance inst_zero : IsPairMap A B f 0 where
  compatible := by intros; simp

instance inst_id {A : Rep k G} : IsPairMap A A (MonoidHom.id G) (ğŸ™ A.V) where
  compatible _ := rfl

instance inst_id' {A : Rep k G} : IsPairMap A A (MonoidHom.id G) LinearMap.id where
  compatible _ := rfl

variable [IsPairMap A B f Ï†]

variable (A B f Ï†) in
@[simps (config := .lemmasOnly)]
noncomputable def cochainsMap :
    inhomogeneousCochains A âŸ¶ inhomogeneousCochains B where
  f i := ModuleCat.ofHom (Ï†.compLeft (Fin i â†’ G)
    âˆ˜â‚— LinearMap.funLeft k A (fun x : Fin i â†’ G => (f âˆ˜ x)))
  comm' i j (hij : _ = _) := by
    subst hij
    ext x
    funext g
    simp only [CochainComplex.of_x, inhomogeneousCochains.d_def, ModuleCat.coe_comp,
      Function.comp_apply]
    simpa [ModuleCat.ofHom, ModuleCat.coe_of, ModuleCat.hom_def, Fin.comp_contractNth]
      using (compatible_apply _ _).symm

@[simp]
lemma cochainsMap_f_apply (n : â„•) (x : (inhomogeneousCochains A).X n) (g : Fin n â†’ G) :
    (cochainsMap A B f Ï†).f n x g = Ï† (x (f âˆ˜ g)) :=
  rfl

@[simp]
lemma cochainsMap_id :
    cochainsMap A A (MonoidHom.id _) (ğŸ™ A.V) = ğŸ™ (inhomogeneousCochains A) := by
  rfl

@[simp]
lemma cochainsMap_id' :
    cochainsMap A A (MonoidHom.id _) LinearMap.id = ğŸ™ (inhomogeneousCochains A) := by
  rfl

@[simp]
lemma cochainsMap_eq_compLeft {A B : Rep k G} (f : A âŸ¶ B) (i : â„•) :
    (cochainsMap A B (MonoidHom.id G) f.hom).f i = f.hom.compLeft _ := by
  ext
  rfl

@[simp]
lemma cochainsMap_comp {k G H K : Type u} [CommRing k] [Group G] [Group H] [Group K]
    (A : Rep k K) (B : Rep k H) (C : Rep k G) (f : H â†’* K) (g : G â†’* H) (Ï† : A â†’â‚—[k] B)
    (Ïˆ : B â†’â‚—[k] C) [IsPairMap A B f Ï†] [IsPairMap B C g Ïˆ] :
    cochainsMap A C (f.comp g) (Ïˆ âˆ˜â‚— Ï†) = cochainsMap A B f Ï† â‰« cochainsMap B C g Ïˆ := by
  rfl

@[simp]
lemma cochainsMap_comp' {k G : Type u} [CommRing k] [Group G]
    {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) :
    cochainsMap A C (MonoidHom.id G) (Ï†.hom â‰« Ïˆ.hom)
      = cochainsMap A B (MonoidHom.id G) Ï†.hom â‰« cochainsMap B C (MonoidHom.id G) Ïˆ.hom := by
  rfl

@[simp]
lemma cochainsMap_zero : cochainsMap A B f 0 = 0 := by rfl

instance cochainsMap_f_map_mono {A B : Rep k G} (Ï† : A âŸ¶ B) [Mono Ï†] (i : â„•) :
    Mono ((cochainsMap A B (MonoidHom.id G) Ï†.hom).f i) :=
  (ModuleCat.mono_iff_injective _).2 <| Function.Injective.comp_left <|
    (Rep.mono_iff_injective Ï†).1 inferInstance

instance cochainsMap_f_map_epi {A B : Rep k G} (Ï† : A âŸ¶ B) [Epi Ï†] (i : â„•) :
    Epi ((cochainsMap A B (MonoidHom.id G) Ï†.hom).f i) :=
  (ModuleCat.epi_iff_surjective _).2 <| Function.Surjective.comp_left <|
    (Rep.epi_iff_surjective Ï†).1 inferInstance

variable (A B f Ï†)
noncomputable abbrev cocyclesMap (n : â„•) :
    groupCohomology.cocycles A n âŸ¶ groupCohomology.cocycles B n :=
  HomologicalComplex.cyclesMap (cochainsMap A B f Ï†) n

theorem cocyclesMap_comp {k G H K : Type u} [CommRing k] [Group G] [Group H] [Group K]
    (A : Rep k K) (B : Rep k H) (C : Rep k G) (f : H â†’* K) (g : G â†’* H) (Ï† : A â†’â‚—[k] B)
    (Ïˆ : B â†’â‚—[k] C) [IsPairMap A B f Ï†] [IsPairMap B C g Ïˆ] (n : â„•) :
    cocyclesMap A C (f.comp g) (Ïˆ.comp Ï†) n = cocyclesMap A B f Ï† n â‰« cocyclesMap B C g Ïˆ n := by
  simp [cocyclesMap, HomologicalComplex.cyclesMap_comp]

noncomputable abbrev cohomologyMap (n : â„•) :
  groupCohomology A n âŸ¶ groupCohomology B n :=
HomologicalComplex.homologyMap (cochainsMap A B f Ï†) n

theorem cohomologyMap_comp {k G H K : Type u} [CommRing k] [Group G] [Group H] [Group K]
    (A : Rep k K) (B : Rep k H) (C : Rep k G) (f : H â†’* K) (g : G â†’* H) (Ï† : A â†’â‚—[k] B)
    (Ïˆ : B â†’â‚—[k] C) [IsPairMap A B f Ï†] [IsPairMap B C g Ïˆ] (n : â„•) :
    cohomologyMap A C (f.comp g) (Ïˆ.comp Ï†) n
      = cohomologyMap A B f Ï† n â‰« cohomologyMap B C g Ïˆ n := by
  rw [cohomologyMap, cochainsMap_comp, HomologicalComplex.homologyMap_comp]

abbrev fOne := Ï†.compLeft G âˆ˜â‚— LinearMap.funLeft k A f
abbrev fTwo := Ï†.compLeft (G Ã— G) âˆ˜â‚— LinearMap.funLeft k A (Prod.map f f)
abbrev fThree := Ï†.compLeft (G Ã— G Ã— G) âˆ˜â‚— LinearMap.funLeft k A (Prod.map f (Prod.map f f))

@[reassoc (attr := simp)]
lemma cochainsMap_f_0_comp_zeroCochainsLEquiv :
    (cochainsMap A B f Ï†).f 0 â‰« (zeroCochainsLEquiv B : (inhomogeneousCochains B).X 0 â†’â‚—[k] B)
      = (zeroCochainsLEquiv A : (inhomogeneousCochains A).X 0 â†’â‚—[k] A) â‰« ModuleCat.ofHom Ï† := by
  ext x
  simp only [cochainsMap_f, Unique.eq_default (f âˆ˜ _)]
  rfl

@[reassoc (attr := simp)]
lemma cochainsMap_f_1_comp_oneCochainsLEquiv :
    (cochainsMap A B f Ï†).f 1 â‰« (oneCochainsLEquiv B : (inhomogeneousCochains B).X 1 â†’â‚—[k] G â†’ B)
      = (oneCochainsLEquiv A).toModuleIso.hom
      â‰« ModuleCat.ofHom (fOne A B f Ï†) := by
  ext x
  simp only [cochainsMap_f, Unique.eq_default (f âˆ˜ _)]
  rfl

@[reassoc (attr := simp)]
lemma cochainsMap_f_2_comp_twoCochainsLEquiv :
    (cochainsMap A B f Ï†).f 2
      â‰« (twoCochainsLEquiv B : (inhomogeneousCochains B).X 2 â†’â‚—[k] G Ã— G â†’ B)
      = (twoCochainsLEquiv A).toModuleIso.hom â‰« ModuleCat.ofHom (fTwo A B f Ï†) := by
  ext x
  funext g
  show Ï† (x _) = Ï† (x _)
  rcongr x
  fin_cases x <;> rfl

@[reassoc (attr := simp)]
lemma cochainsMap_f_3_comp_threeCochainsLEquiv :
    (cochainsMap A B f Ï†).f 3
      â‰« (threeCochainsLEquiv B : (inhomogeneousCochains B).X 3 â†’â‚—[k] G Ã— G Ã— G â†’ B)
      = (threeCochainsLEquiv A).toModuleIso.hom â‰« ModuleCat.ofHom (fThree A B f Ï†) := by
  ext x
  funext g
  show Ï† (x _) = Ï† (x _)
  rcongr x
  fin_cases x <;> rfl

open ShortComplex

def mapH0 : ModuleCat.of k (H0 A) âŸ¶ ModuleCat.of k (H0 B) :=
  LinearMap.codRestrict _ (Ï† âˆ˜â‚— A.Ï.invariants.subtype) fun âŸ¨c, hcâŸ© g => by
    simp [â† compatible_apply (f := f) g c, hc (f g), ModuleCat.coe_of]

@[simp]
theorem mapH0_id : mapH0 A A (MonoidHom.id _) (ğŸ™ A.V) = ğŸ™ _ := by
  rfl

theorem mapH0_comp {k G H K : Type u} [CommRing k] [Group G] [Group H] [Group K]
    (A : Rep k K) (B : Rep k H) (C : Rep k G) (f : H â†’* K) (g : G â†’* H) (Ï† : A â†’â‚—[k] B)
    (Ïˆ : B â†’â‚—[k] C) [IsPairMap A B f Ï†] [IsPairMap B C g Ïˆ] :
    mapH0 A C (f.comp g) (Ïˆ.comp Ï†) = mapH0 A B f Ï† â‰« mapH0 B C g Ïˆ := rfl

theorem mapH0_eq_invariantsFunctor_map {A B : Rep k G} (f : A âŸ¶ B) :
    mapH0 A B (MonoidHom.id G) f.hom = (invariantsFunctor k G).map f := by
  rfl

instance mono_mapH0_of_mono {A B : Rep k G} (f : A âŸ¶ B) [Mono f] :
    Mono (mapH0 A B (MonoidHom.id G) f.hom) :=
  (inferInstanceAs (Mono <| (invariantsFunctor k G).map f))

@[reassoc (attr := simp)]
theorem cocyclesMap_comp_isoZeroCocycles_hom :
    cocyclesMap A B f Ï† 0 â‰« (isoZeroCocycles B).hom
      = (isoZeroCocycles A).hom â‰« mapH0 A B f Ï† := by
  rw [â† Iso.eq_comp_inv, Category.assoc, â† Iso.inv_comp_eq,
    â† cancel_mono (HomologicalComplex.iCycles _ _)]
  simp only [CochainComplex.of_x, cocyclesMap, Category.assoc, HomologicalComplex.cyclesMap_i,
    isoZeroCocycles_inv_comp_iCocycles_assoc, ModuleCat.of_coe, LinearEquiv.toModuleIso_inv,
    isoZeroCocycles_inv_comp_iCocycles]
  rfl

@[reassoc (attr := simp)]
theorem cohomologyMap_comp_isoH0_hom :
    cohomologyMap A B f Ï† 0 â‰« (isoH0 B).hom = (isoH0 A).hom â‰« mapH0 A B f Ï† := by
  simp [â† cancel_epi (groupCohomologyÏ€ _ _)]

@[simps]
def mapShortComplexH1 :
    shortComplexH1 A âŸ¶ shortComplexH1 B where
  Ï„â‚ := ModuleCat.ofHom Ï†
  Ï„â‚‚ := ModuleCat.ofHom (fOne A B f Ï†)
  Ï„â‚ƒ := ModuleCat.ofHom (fTwo A B f Ï†)
  commâ‚â‚‚ := by
    ext x
    funext g
    dsimp [shortComplexH1, dZero]
    simp [ModuleCat.coe_of, ModuleCat.hom_def, compatible_apply]
  commâ‚‚â‚ƒ := by
    ext x
    funext g
    dsimp [shortComplexH1, dOne]
    simp [ModuleCat.coe_of, ModuleCat.hom_def, compatible_apply]

@[simp]
theorem mapShortComplexH1_id :
    mapShortComplexH1 A A (MonoidHom.id _) (ğŸ™ A.V) = ğŸ™ _ := by
  rfl

theorem mapShortComplexH1_comp {k G H K : Type u} [CommRing k] [Group G] [Group H] [Group K]
    (A : Rep k K) (B : Rep k H) (C : Rep k G) (f : H â†’* K) (g : G â†’* H) (Ï† : A â†’â‚—[k] B)
    (Ïˆ : B â†’â‚—[k] C) [IsPairMap A B f Ï†] [IsPairMap B C g Ïˆ] :
    mapShortComplexH1 A C (f.comp g) (Ïˆ.comp Ï†)
      = mapShortComplexH1 A B f Ï† â‰« mapShortComplexH1 B C g Ïˆ := rfl

noncomputable abbrev mapOneCocycles :
    ModuleCat.of k (oneCocycles A) âŸ¶ ModuleCat.of k (oneCocycles B) :=
  ShortComplex.cyclesMap' (mapShortComplexH1 A B f Ï†) (shortComplexH1 A).moduleCatLeftHomologyData
    (shortComplexH1 B).moduleCatLeftHomologyData

noncomputable abbrev mapH1 : ModuleCat.of k (H1 A) âŸ¶ ModuleCat.of k (H1 B) :=
  ShortComplex.leftHomologyMap' (mapShortComplexH1 A B f Ï†)
    (shortComplexH1 A).moduleCatLeftHomologyData
    (shortComplexH1 B).moduleCatLeftHomologyData

@[simp]
theorem mapH1_id : mapH1 A A (MonoidHom.id _) (ğŸ™ A.V) = ğŸ™ _ := by
  simp only [mapH1, shortComplexH1, mapShortComplexH1_id, leftHomologyMap'_id]
  rfl

theorem mapH1_comp {k G H K : Type u} [CommRing k] [Group G] [Group H] [Group K]
    (A : Rep k K) (B : Rep k H) (C : Rep k G) (f : H â†’* K) (g : G â†’* H) (Ï† : A â†’â‚—[k] B)
    (Ïˆ : B â†’â‚—[k] C) [IsPairMap A B f Ï†] [IsPairMap B C g Ïˆ] :
    mapH1 A C (f.comp g) (Ïˆ.comp Ï†) = mapH1 A B f Ï† â‰« mapH1 B C g Ïˆ := by
  simpa [mapH1, shortComplexH1, mapShortComplexH1_comp] using leftHomologyMap'_comp _ _ _ _ _

@[simp]
lemma subtype_comp_mapOneCocycles :
    (oneCocycles B).subtype âˆ˜â‚— mapOneCocycles A B f Ï†
      = fOne A B f Ï† âˆ˜â‚— (oneCocycles A).subtype :=
  ShortComplex.cyclesMap'_i (mapShortComplexH1 A B f Ï†) (moduleCatLeftHomologyData _)
    (moduleCatLeftHomologyData _)

@[simp]
lemma H1Ï€_comp_mapH1 :
    mapH1 A B f Ï† âˆ˜â‚— H1Ï€ A = H1Ï€ B âˆ˜â‚— mapOneCocycles A B f Ï† :=
  leftHomologyÏ€_naturality' (mapShortComplexH1 A B f Ï†) _ _

@[reassoc (attr := simp)]
lemma cocyclesMap_comp_isoOneCocycles_hom :
    cocyclesMap A B f Ï† 1 â‰« (isoOneCocycles B).hom
      = (isoOneCocycles A).hom â‰« mapOneCocycles A B f Ï† := by
  simp_rw [â† cancel_mono (moduleCatLeftHomologyData (shortComplexH1 B)).i, mapOneCocycles,
      Category.assoc, cyclesMap'_i, isoOneCocycles, â† Category.assoc]
  simp

@[reassoc (attr := simp)]
lemma cohomologyMap_comp_isoH1_hom :
    cohomologyMap A B f Ï† 1 â‰« (isoH1 B).hom = (isoH1 A).hom â‰« mapH1 A B f Ï† := by
  simpa [â† cancel_epi (groupCohomologyÏ€ _ _), mapH1, Category.assoc]
    using (leftHomologyÏ€_naturality' (mapShortComplexH1 A B f Ï†)
    (moduleCatLeftHomologyData _) (moduleCatLeftHomologyData _)).symm

@[simps]
def mapShortComplexH2 :
    shortComplexH2 A âŸ¶ shortComplexH2 B where
  Ï„â‚ := ModuleCat.ofHom (fOne A B f Ï†)
  Ï„â‚‚ := ModuleCat.ofHom (fTwo A B f Ï†)
  Ï„â‚ƒ := ModuleCat.ofHom (fThree A B f Ï†)
  commâ‚â‚‚ := by
    ext x
    funext g
    dsimp [shortComplexH2, dOne]
    simp [ModuleCat.coe_of, ModuleCat.hom_def, compatible_apply]
  commâ‚‚â‚ƒ := by
    ext x
    funext g
    dsimp [shortComplexH2, dTwo]
    simp [ModuleCat.coe_of, ModuleCat.hom_def, compatible_apply]

@[simp]
theorem mapShortComplexH2_id :
    mapShortComplexH2 A A (MonoidHom.id _) (ğŸ™ A.V) = ğŸ™ _ := by
  rfl

theorem mapShortComplexH2_comp {k G H K : Type u} [CommRing k] [Group G] [Group H] [Group K]
    (A : Rep k K) (B : Rep k H) (C : Rep k G) (f : H â†’* K) (g : G â†’* H) (Ï† : A â†’â‚—[k] B)
    (Ïˆ : B â†’â‚—[k] C) [IsPairMap A B f Ï†] [IsPairMap B C g Ïˆ] :
    mapShortComplexH2 A C (f.comp g) (Ïˆ.comp Ï†)
      = mapShortComplexH2 A B f Ï† â‰« mapShortComplexH2 B C g Ïˆ := rfl

noncomputable abbrev mapTwoCocycles :
    ModuleCat.of k (twoCocycles A) âŸ¶ ModuleCat.of k (twoCocycles B) :=
  ShortComplex.cyclesMap' (mapShortComplexH2 A B f Ï†) (shortComplexH2 A).moduleCatLeftHomologyData
    (shortComplexH2 B).moduleCatLeftHomologyData

noncomputable abbrev mapH2 : ModuleCat.of k (H2 A) âŸ¶ ModuleCat.of k (H2 B) :=
  ShortComplex.leftHomologyMap' (mapShortComplexH2 A B f Ï†)
    (shortComplexH2 A).moduleCatLeftHomologyData
    (shortComplexH2 B).moduleCatLeftHomologyData

@[simp]
theorem mapH2_id : mapH2 A A (MonoidHom.id _) (ğŸ™ A.V) = ğŸ™ _ := by
  simp only [mapH2, shortComplexH2, mapShortComplexH2_id, leftHomologyMap'_id]
  rfl

theorem mapH2_comp {k G H K : Type u} [CommRing k] [Group G] [Group H] [Group K]
    (A : Rep k K) (B : Rep k H) (C : Rep k G) (f : H â†’* K) (g : G â†’* H) (Ï† : A â†’â‚—[k] B)
    (Ïˆ : B â†’â‚—[k] C) [IsPairMap A B f Ï†] [IsPairMap B C g Ïˆ] :
    mapH2 A C (f.comp g) (Ïˆ.comp Ï†) = mapH2 A B f Ï† â‰« mapH2 B C g Ïˆ := by
  simpa [mapH2, shortComplexH2, mapShortComplexH2_comp] using leftHomologyMap'_comp _ _ _ _ _

@[simp]
lemma subtype_comp_mapTwoCocycles :
    (twoCocycles B).subtype âˆ˜â‚— mapTwoCocycles A B f Ï†
      = fTwo A B f Ï† âˆ˜â‚— (twoCocycles A).subtype :=
  ShortComplex.cyclesMap'_i (mapShortComplexH2 A B f Ï†) (moduleCatLeftHomologyData _)
    (moduleCatLeftHomologyData _)

@[simp]
lemma H2Ï€_comp_mapH2 :
    mapH2 A B f Ï† âˆ˜â‚— H2Ï€ A = H2Ï€ B âˆ˜â‚— mapTwoCocycles A B f Ï† :=
  leftHomologyÏ€_naturality' (mapShortComplexH2 A B f Ï†) _ _

@[reassoc (attr := simp)]
lemma cocyclesMap_comp_isoTwoCocycles_hom :
    cocyclesMap A B f Ï† 2 â‰« (isoTwoCocycles B).hom
      = (isoTwoCocycles A).hom â‰« mapTwoCocycles A B f Ï† := by
  simp_rw [â† cancel_mono (moduleCatLeftHomologyData (shortComplexH2 B)).i, mapTwoCocycles,
      Category.assoc, cyclesMap'_i, isoTwoCocycles, â† Category.assoc]
  simp

@[reassoc (attr := simp)]
lemma cohomologyMap_comp_isoH2_hom :
    cohomologyMap A B f Ï† 2 â‰« (isoH2 B).hom = (isoH2 A).hom â‰« mapH2 A B f Ï† := by
  simpa [â† cancel_epi (groupCohomologyÏ€ _ _), mapH2, Category.assoc]
    using (leftHomologyÏ€_naturality' (mapShortComplexH2 A B f Ï†)
    (moduleCatLeftHomologyData _) (moduleCatLeftHomologyData _)).symm

end IsPairMap

open IsPairMap

variable (k G) in
@[simps]
noncomputable def cochainsFunctor : Rep k G â¥¤ CochainComplex (ModuleCat k) â„• where
  obj A := inhomogeneousCochains A
  map f := cochainsMap _ _ (MonoidHom.id _) f.hom
  map_id _ := cochainsMap_id
  map_comp {X Y Z} Ï† Ïˆ := cochainsMap_comp X Y Z (MonoidHom.id G) (MonoidHom.id G) Ï†.hom Ïˆ.hom

instance : (cochainsFunctor k G).PreservesZeroMorphisms where
instance : (cochainsFunctor k G).Additive where

variable (k G) in
@[simps]
noncomputable def functor (n : â„•) : Rep k G â¥¤ ModuleCat k where
  obj A := groupCohomology A n
  map {A B} Ï† := cohomologyMap A B (MonoidHom.id _) Ï†.hom n
  map_id A := HomologicalComplex.homologyMap_id _ _
  map_comp f g := by
    simp only [â† HomologicalComplex.homologyMap_comp]
    rfl

instance (n : â„•) : (functor k G n).PreservesZeroMorphisms where
  map_zero _ _ := by simp [cohomologyMap]

open ShortComplex

variable {X : ShortComplex (Rep k G)} (C D : ChainComplex (Rep k G) â„•) (f : C âŸ¶ D) [Epi f]

def mapShortExact (H : ShortExact X) :
    ShortExact (X.map (cochainsFunctor k G)) :=
  HomologicalComplex.shortExact_of_degreewise_shortExact _ fun i => {
    exact := by
      rw [ShortComplex.moduleCat_exact_iff_range_eq_ker]
      have : LinearMap.range X.f.hom = LinearMap.ker X.g.hom :=
        (H.exact.map (forgetâ‚‚ (Rep k G) (ModuleCat k))).moduleCat_range_eq_ker
      show LinearMap.range (LinearMap.compLeft X.f.hom (Fin i â†’ G))
        = LinearMap.ker (LinearMap.compLeft X.g.hom (Fin i â†’ G))
      rw [LinearMap.range_compLeft, LinearMap.ker_compLeft, this]
    mono_f := letI := H.2; cochainsMap_f_map_mono X.f i
    epi_g := letI := H.3; cochainsMap_f_map_epi X.g i }

/-- The short complex  `Hâ±(G, Xâ‚ƒ) âŸ¶ HÊ²(G, Xâ‚) âŸ¶ HÊ²(G, Xâ‚‚)`-/
noncomputable abbrev cohomologyShortComplexâ‚
    (H : ShortExact X) {i j : â„•} (hij : i + 1 = j) :=
  ShortComplex.mk _ _ ((mapShortExact H).Î´_comp i j hij)

/-- The short complex `Hâ±(G, Xâ‚) âŸ¶ Hâ±(G, Xâ‚‚) âŸ¶ Hâ±(G, Xâ‚ƒ)`. -/
noncomputable abbrev cohomologyShortComplexâ‚‚ (H : ShortExact X) (i : â„•) :=
  ShortComplex.mk (cohomologyMap X.Xâ‚ X.Xâ‚‚ (MonoidHom.id G) X.f.hom i)
    (cohomologyMap X.Xâ‚‚ X.Xâ‚ƒ (MonoidHom.id G) X.g.hom i) <| by
      have : X.g.hom âˆ˜â‚— X.f.hom = 0 := Action.Hom.ext_iff.1 X.zero
      simp [â† HomologicalComplex.homologyMap_comp, â† cochainsMap_comp, this]

/-- The short complex `Hâ±(G, Xâ‚‚) âŸ¶ Hâ±(G, Xâ‚ƒ) âŸ¶ HÊ²(G, Xâ‚)`. -/
noncomputable abbrev cohomologyShortComplexâ‚ƒ (H : ShortExact X) {i j : â„•} (hij : i + 1 = j) :=
  ShortComplex.mk _ _ ((mapShortExact H).comp_Î´ i j hij)

/-- Exactness of `Hâ±(G, Xâ‚ƒ) âŸ¶ HÊ²(G, Xâ‚) âŸ¶ HÊ²(G, Xâ‚‚)`. -/
lemma cohomology_exactâ‚ (H : ShortExact X) {i j : â„•} (hij : i + 1 = j) :
    (cohomologyShortComplexâ‚ H hij).Exact :=
  (mapShortExact H).homology_exactâ‚ i j hij

/-- Exactness of `Hâ±(G, Xâ‚) âŸ¶ Hâ±(G, Xâ‚‚) âŸ¶ Hâ±(G, Xâ‚ƒ)`. -/
lemma cohomology_exactâ‚‚ (H : ShortExact X) (i : â„•) :
    (cohomologyShortComplexâ‚‚ H i).Exact :=
  (mapShortExact H).homology_exactâ‚‚ i

/--  Exactness of `Hâ±(G, Xâ‚‚) âŸ¶ Hâ±(G, Xâ‚ƒ) âŸ¶ HÊ²(G, Xâ‚)`. -/
lemma cohomology_exactâ‚ƒ (H : ShortExact X) {i j : â„•} (hij : i + 1 = j) :
    (cohomologyShortComplexâ‚ƒ H hij).Exact :=
  (mapShortExact H).homology_exactâ‚ƒ i j hij

theorem Î´_apply_aux (H : ShortExact X) (n : â„•) (y : (Fin n â†’ G) â†’ X.Xâ‚‚)
    (x : (Fin (n + 1) â†’ G) â†’ X.Xâ‚) (hx : X.f.hom âˆ˜ x = inhomogeneousCochains.d X.Xâ‚‚ n y) :
    inhomogeneousCochains.d X.Xâ‚ (n + 1) x = 0 := by
  letI := H.2
  change (cochainsMap X.Xâ‚ X.Xâ‚‚ (MonoidHom.id G) _).f _ _ = _ at hx
  have := congr($((cochainsMap X.Xâ‚ X.Xâ‚‚ (MonoidHom.id G) X.f.hom).comm (n + 1) (n + 2)) x)
  simp only [CochainComplex.of_x, inhomogeneousCochains.d_def, ModuleCat.coe_comp,
    Function.comp_apply] at this hx
  rw [hx] at this
  apply (ModuleCat.mono_iff_injective ((cochainsMap X.Xâ‚ X.Xâ‚‚
    (MonoidHom.id G) X.f.hom).f (n + 2))).1
  Â· infer_instance
  Â· simp only [CochainComplex.of_x, map_zero]
    exact this â–¸ congr($(inhomogeneousCochains.d_comp_d X.Xâ‚‚ n) y)

theorem Î´_apply (H : ShortExact X) (n : â„•)
    (z : (Fin n â†’ G) â†’ X.Xâ‚ƒ) (hz : inhomogeneousCochains.d X.Xâ‚ƒ n z = 0)
    (y : (Fin n â†’ G) â†’ X.Xâ‚‚) (hy : (cochainsMap X.Xâ‚‚ X.Xâ‚ƒ (MonoidHom.id G) X.g.hom).f n y = z)
    (x : (Fin (n + 1) â†’ G) â†’ X.Xâ‚)
    (hx : X.f.hom âˆ˜ x = inhomogeneousCochains.d X.Xâ‚‚ n y) :
    (mapShortExact H).Î´ n (n + 1) rfl (groupCohomologyÏ€ X.Xâ‚ƒ n <|
      (cocyclesIso X.Xâ‚ƒ n).inv âŸ¨z, hzâŸ©) = groupCohomologyÏ€ X.Xâ‚ (n + 1)
      ((cocyclesIso X.Xâ‚ (n + 1)).inv âŸ¨x, Î´_apply_aux H n y x hxâŸ©) := by
  simp_rw [cocyclesIso_inv_eq]
  exact ShortExact.Î´_apply (mapShortExact H) n _ rfl z (by simpa using hz) y hy x
    (by simpa using hx) (n + 2) (by simp)

noncomputable def Î´â‚€ (H : ShortExact X) :
    ModuleCat.of k (H0 X.Xâ‚ƒ) âŸ¶ ModuleCat.of k (H1 X.Xâ‚) :=
  (isoH0 X.Xâ‚ƒ).inv â‰« (mapShortExact H).Î´ 0 1 rfl â‰« (isoH1 X.Xâ‚).hom

theorem Î´â‚€_apply_aux (H : ShortExact X) (y : X.Xâ‚‚) (x : G â†’ X.Xâ‚)
    (hx : X.f.hom âˆ˜ x = dZero X.Xâ‚‚ y) :
    dOne X.Xâ‚ x = 0 := by
  have h0 := Î´_apply_aux H 0 ((zeroCochainsLEquiv X.Xâ‚‚).symm y) ((oneCochainsLEquiv X.Xâ‚).symm x)
  have hy := congr($((CommSq.horiz_inv âŸ¨(shortComplexH1Iso X.Xâ‚‚).hom.commâ‚â‚‚âŸ©).w) y)
  have h := congr($((Iso.eq_inv_comp _).2 (shortComplexH1Iso X.Xâ‚).hom.commâ‚‚â‚ƒ) x)
  simp only [ModuleCat.coe_comp, Function.comp_apply] at h0 hy
  exact h.trans <| (twoCochainsLEquiv X.Xâ‚).map_eq_zero_iff.2 <| h0 (hy.trans <| hx â–¸ rfl).symm

theorem Î´â‚€_apply (H : ShortExact X)
    (z : X.Xâ‚ƒ) (hz : z âˆˆ X.Xâ‚ƒ.Ï.invariants) (y : X.Xâ‚‚) (hy : X.g.hom y = z)
    (x : G â†’ X.Xâ‚) (hx : X.f.hom âˆ˜ x = dZero X.Xâ‚‚ y) :
    Î´â‚€ H âŸ¨z, hzâŸ© = H1Ï€ X.Xâ‚ âŸ¨x, Î´â‚€_apply_aux H y x hxâŸ© := by
  have h0z : (inhomogeneousCochains.d X.Xâ‚ƒ 0) ((zeroCochainsLEquiv X.Xâ‚ƒ).symm z) = 0 := by
    have := congr($((LinearEquiv.symm_comp_eq_comp_symm_iff _ _).2 (dZero_comp_eq X.Xâ‚ƒ)) z)
    simp_all [â† dZero_ker_eq_invariants, ModuleCat.coe_of]
  have hxy : X.f.hom âˆ˜ (oneCochainsLEquiv X.Xâ‚).symm x = inhomogeneousCochains.d X.Xâ‚‚ 0
      ((zeroCochainsLEquiv X.Xâ‚‚).symm y) := by
    have := (congr($((LinearEquiv.symm_comp_eq_comp_symm_iff _ _).2 (dZero_comp_eq X.Xâ‚‚)) y)).symm
    ext i
    simp_all only [CochainComplex.of_x, ModuleCat.coe_of,
      inhomogeneousCochains.d_def, LinearMap.coe_comp, LinearEquiv.coe_coe, Function.comp_apply]
    simp [â† hx, oneCochainsLEquiv]
  have := congr((isoH1 X.Xâ‚).hom $(Î´_apply H 0 ((zeroCochainsLEquiv X.Xâ‚ƒ).symm z) h0z
    ((zeroCochainsLEquiv X.Xâ‚‚).symm y) (hy â–¸ rfl) ((oneCochainsLEquiv X.Xâ‚).symm x) hxy))
  convert this
  Â· exact cocyclesIso_0_eq X.Xâ‚ƒ â–¸ rfl
  Â· have := LinearMap.ext_iff.1 ((Iso.inv_comp_eq _).2 (groupCohomologyÏ€_comp_isoH1_hom X.Xâ‚))
    simp_all only [cocyclesIso_1_eq X.Xâ‚, Iso.trans_inv, ModuleCat.hom_def,
      ModuleCat.coe_of, LinearEquiv.toModuleIso_inv, ModuleCat.comp_def, LinearMap.coe_comp,
      Function.comp_apply]
    rfl

open Limits

theorem epi_Î´â‚€ (X : ShortComplex (Rep k G)) (H : ShortExact X)
    (h1 : IsZero (ModuleCat.of k <| H1 X.Xâ‚‚)) : Epi (Î´â‚€ H) := by
  letI : Epi ((mapShortExact H).Î´ 0 1 rfl) := (mapShortExact H).epi_Î´ _ _ rfl
    (h1.of_iso (isoH1 X.Xâ‚‚))
  exact epi_comp _ _

noncomputable def Î´â‚ {X : ShortComplex (Rep k G)} (H : ShortExact X) :
    ModuleCat.of k (H1 X.Xâ‚ƒ) âŸ¶ ModuleCat.of k (H2 X.Xâ‚) :=
  (isoH1 X.Xâ‚ƒ).inv â‰« (mapShortExact H).Î´ 1 2 rfl â‰« (isoH2 X.Xâ‚).hom

theorem Î´â‚_apply_aux {X : ShortComplex (Rep k G)} (H : ShortExact X) (y : G â†’ X.Xâ‚‚)
    (x : G Ã— G â†’ X.Xâ‚) (hx : X.f.hom âˆ˜ x = dOne X.Xâ‚‚ y) :
    dTwo X.Xâ‚ x = 0 := by
  have h1 := Î´_apply_aux H 1 ((oneCochainsLEquiv X.Xâ‚‚).symm y) ((twoCochainsLEquiv X.Xâ‚).symm x)
  have hy := congr($((CommSq.horiz_inv âŸ¨(shortComplexH2Iso X.Xâ‚‚).hom.commâ‚â‚‚âŸ©).w) y)
  have h := congr($((Iso.eq_inv_comp _).2 (shortComplexH2Iso X.Xâ‚).hom.commâ‚‚â‚ƒ) x)
  simp only [ModuleCat.coe_comp, Function.comp_apply] at h1 hy
  exact h.trans <| (threeCochainsLEquiv X.Xâ‚).map_eq_zero_iff.2 <| h1 (hy.trans <| hx â–¸ rfl).symm

theorem Î´â‚_apply (X : ShortComplex (Rep k G)) (H : ShortExact X)
    (z : G â†’ X.Xâ‚ƒ) (hz : z âˆˆ oneCocycles X.Xâ‚ƒ) (y : G â†’ X.Xâ‚‚) (hy : X.g.hom âˆ˜ y = z)
    (x : G Ã— G â†’ X.Xâ‚) (hx : X.f.hom âˆ˜ x = dOne X.Xâ‚‚ y) :
    Î´â‚ H (H1Ï€ X.Xâ‚ƒ âŸ¨z, hzâŸ©) = H2Ï€ X.Xâ‚ âŸ¨x, Î´â‚_apply_aux H y x hxâŸ© := by
  have h1z : (inhomogeneousCochains.d X.Xâ‚ƒ 1) ((oneCochainsLEquiv X.Xâ‚ƒ).symm z) = 0 := by
    have := congr($((LinearEquiv.symm_comp_eq_comp_symm_iff _ _).2 (dOne_comp_eq X.Xâ‚ƒ)) z)
    simp_all [ModuleCat.coe_of, oneCocycles]
  have hxy : X.f.hom âˆ˜ (twoCochainsLEquiv X.Xâ‚).symm x
      = inhomogeneousCochains.d X.Xâ‚‚ 1 ((oneCochainsLEquiv X.Xâ‚‚).symm y) := by
    have := (congr($((LinearEquiv.symm_comp_eq_comp_symm_iff _ _).2 (dOne_comp_eq X.Xâ‚‚)) y)).symm
    ext i
    simp_all only [CochainComplex.of_x, ModuleCat.coe_of,
      inhomogeneousCochains.d_def, LinearMap.coe_comp, LinearEquiv.coe_coe, Function.comp_apply]
    simp [â† hx, twoCochainsLEquiv]
  have := congr((isoH2 X.Xâ‚).hom $(Î´_apply H 1 ((oneCochainsLEquiv X.Xâ‚ƒ).symm z) h1z
    ((oneCochainsLEquiv X.Xâ‚‚).symm y) (hy â–¸ rfl) ((twoCochainsLEquiv X.Xâ‚).symm x) hxy))
  convert this
  Â· have := congr($((CommSq.vert_inv âŸ¨groupCohomologyÏ€_comp_isoH1_hom X.Xâ‚ƒâŸ©).w) âŸ¨z, hzâŸ©)
    rw [cocyclesIso_1_eq]
    exact this â–¸ rfl
  Â· have := LinearMap.ext_iff.1 ((Iso.inv_comp_eq _).2 (groupCohomologyÏ€_comp_isoH2_hom X.Xâ‚))
    simp_all only [cocyclesIso_2_eq X.Xâ‚, Iso.trans_inv, ModuleCat.hom_def,
      ModuleCat.coe_of, LinearEquiv.toModuleIso_inv, ModuleCat.comp_def, LinearMap.coe_comp,
      Function.comp_apply]
    rfl

theorem epi_Î´â‚ (X : ShortComplex (Rep k G)) (H : ShortExact X)
    (h2 : IsZero (ModuleCat.of k <| H2 X.Xâ‚‚)) : Epi (Î´â‚ H) := by
  letI : Epi ((mapShortExact H).Î´ 1 2 rfl) := (mapShortExact H).epi_Î´ _ _ rfl
    (h2.of_iso (isoH2 X.Xâ‚‚))
  exact epi_comp _ _

/-- The short complex `Xâ‚á´³ âŸ¶ Xâ‚‚á´³ âŸ¶ Xâ‚ƒá´³`. -/
noncomputable abbrev H0ShortComplexâ‚‚ (H : ShortExact X) :=
  ShortComplex.mk (mapH0 _ _ (MonoidHom.id G) X.f.hom) (mapH0 _ _ (MonoidHom.id G) X.g.hom) <| by
    ext x; apply Subtype.ext; exact congr(hom $(X.zero) x.1)

noncomputable def isoH0ShortComplexâ‚‚ (H : ShortExact X) :
    cohomologyShortComplexâ‚‚ H 0 â‰… H0ShortComplexâ‚‚ H :=
  isoMk (isoH0 _) (isoH0 _) (isoH0 _) (cohomologyMap_comp_isoH0_hom _ _ _ _).symm
    (cohomologyMap_comp_isoH0_hom _ _ _ _).symm

theorem H0ShortComplexâ‚‚_exact (H : ShortExact X) :
    (H0ShortComplexâ‚‚ H).Exact :=
  exact_of_iso (isoH0ShortComplexâ‚‚ H) (cohomology_exactâ‚‚ _ _)

/-- The short complex `Xâ‚‚á´³ âŸ¶ Xâ‚ƒá´³ âŸ¶ HÂ¹(G, Xâ‚)`. -/
noncomputable abbrev H0ShortComplexâ‚ƒ (H : ShortExact X) :=
  ShortComplex.mk (mapH0 _ _ (MonoidHom.id G) X.g.hom) (Î´â‚€ H) <| by
    rw [Î´â‚€, â† Category.assoc, (CommSq.vert_inv âŸ¨cohomologyMap_comp_isoH0_hom X.Xâ‚‚ X.Xâ‚ƒ
      (MonoidHom.id G) X.g.homâŸ©).w]
    simpa using (mapShortExact H).comp_Î´_assoc 0 1 rfl _

noncomputable def isoH0ShortComplexâ‚ƒ (H : ShortExact X) :
    cohomologyShortComplexâ‚ƒ H (j := 1) rfl â‰… H0ShortComplexâ‚ƒ H :=
  isoMk (isoH0 _) (isoH0 _) (isoH1 _) (cohomologyMap_comp_isoH0_hom _ _ _ _).symm (by simp [Î´â‚€])

theorem H0ShortComplexâ‚ƒ_exact (H : ShortExact X) :
    (H0ShortComplexâ‚ƒ H).Exact :=
  exact_of_iso (isoH0ShortComplexâ‚ƒ H) (cohomology_exactâ‚ƒ _ _)

/-- The short complex  `Xâ‚ƒá´³ âŸ¶ HÂ¹(G, Xâ‚) âŸ¶ HÂ¹(G, Xâ‚‚)`-/
noncomputable abbrev H1ShortComplexâ‚ (H : ShortExact X) :=
  ShortComplex.mk (Î´â‚€ H) (mapH1 X.Xâ‚ X.Xâ‚‚ (MonoidHom.id G) X.f.hom) <| by
    simpa [Î´â‚€, ModuleCat.ofHom, â† cohomologyMap_comp_isoH1_hom]
      using (mapShortExact H).Î´_comp_assoc 0 1 rfl _

noncomputable def isoH1ShortComplexâ‚ (H : ShortExact X) :
    cohomologyShortComplexâ‚ H (i := 0) rfl â‰… H1ShortComplexâ‚ H :=
  isoMk (isoH0 _) (isoH1 _) (isoH1 _) (by simp [Î´â‚€]) (cohomologyMap_comp_isoH1_hom _ _ _ _).symm

theorem H1ShortComplexâ‚_exact (H : ShortExact X) :
    (H1ShortComplexâ‚ H).Exact :=
  exact_of_iso (isoH1ShortComplexâ‚ H) (cohomology_exactâ‚ _ _)

/-- The short complex `HÂ¹(G, Xâ‚) âŸ¶ HÂ¹(G, Xâ‚‚) âŸ¶ HÂ¹(G, Xâ‚ƒ)`. -/
noncomputable abbrev H1ShortComplexâ‚‚ (H : ShortExact X) :=
  ShortComplex.mk (mapH1 X.Xâ‚ X.Xâ‚‚ (MonoidHom.id G) X.f.hom)
    (mapH1 X.Xâ‚‚ X.Xâ‚ƒ (MonoidHom.id G) X.g.hom) <| by
      suffices mapH1 X.Xâ‚ X.Xâ‚ƒ (MonoidHom.id G) (X.f â‰« X.g).hom = 0 by
        rw [â† mapH1_comp]
        exact this
      rw [X.zero]
      exact leftHomologyMap'_zero _ _

noncomputable def isoH1ShortComplexâ‚‚ (H : ShortExact X) :
    cohomologyShortComplexâ‚‚ H 1 â‰… H1ShortComplexâ‚‚ H :=
  isoMk (isoH1 _) (isoH1 _) (isoH1 _) (cohomologyMap_comp_isoH1_hom _ _ _ _).symm
    (cohomologyMap_comp_isoH1_hom _ _ _ _).symm

theorem H1ShortComplexâ‚‚_exact (H : ShortExact X) :
    (H1ShortComplexâ‚‚ H).Exact :=
  exact_of_iso (isoH1ShortComplexâ‚‚ H) (cohomology_exactâ‚‚ _ _)

/-- The short complex `HÂ¹(G, Xâ‚‚) âŸ¶ HÂ¹(G, Xâ‚ƒ) âŸ¶ HÂ²(G, Xâ‚)`. -/
noncomputable abbrev H1ShortComplexâ‚ƒ (H : ShortExact X) :=
  ShortComplex.mk (mapH1 X.Xâ‚‚ X.Xâ‚ƒ (MonoidHom.id G) X.g.hom) (Î´â‚ H) <| by
    rw [Î´â‚, â† Category.assoc, (CommSq.vert_inv âŸ¨cohomologyMap_comp_isoH1_hom X.Xâ‚‚ X.Xâ‚ƒ
      (MonoidHom.id G) X.g.homâŸ©).w]
    simpa using (mapShortExact H).comp_Î´_assoc 1 2 rfl _

noncomputable def isoH1ShortComplexâ‚ƒ (H : ShortExact X) :
    cohomologyShortComplexâ‚ƒ H (i := 1) rfl â‰… H1ShortComplexâ‚ƒ H :=
  isoMk (isoH1 _) (isoH1 _) (isoH2 _) (cohomologyMap_comp_isoH1_hom _ _ _ _).symm (by simp [Î´â‚])

theorem H1ShortComplexâ‚ƒ_exact (H : ShortExact X) :
    (H1ShortComplexâ‚ƒ H).Exact :=
  exact_of_iso (isoH1ShortComplexâ‚ƒ H) (cohomology_exactâ‚ƒ _ _)

/-- The short complex  `HÂ¹(G, Xâ‚ƒ) âŸ¶ HÂ²(G, Xâ‚) âŸ¶ HÂ²(G, Xâ‚‚)`-/
noncomputable abbrev H2ShortComplexâ‚ (H : ShortExact X) :=
  ShortComplex.mk (Î´â‚ H) (mapH2 X.Xâ‚ X.Xâ‚‚ (MonoidHom.id G) X.f.hom) <| by
    simpa [Î´â‚, ModuleCat.ofHom, â† cohomologyMap_comp_isoH2_hom]
      using (mapShortExact H).Î´_comp_assoc 1 2 rfl _

noncomputable def isoH2ShortComplexâ‚ (H : ShortExact X) :
    cohomologyShortComplexâ‚ H (i := 1) rfl â‰… H2ShortComplexâ‚ H :=
  isoMk (isoH1 _) (isoH2 _) (isoH2 _) (by simp [Î´â‚]) (cohomologyMap_comp_isoH2_hom _ _ _ _).symm

theorem H2ShortComplexâ‚_exact (H : ShortExact X) :
    (H2ShortComplexâ‚ H).Exact :=
  exact_of_iso (isoH2ShortComplexâ‚ H) (cohomology_exactâ‚ _ _)

end groupCohomology
