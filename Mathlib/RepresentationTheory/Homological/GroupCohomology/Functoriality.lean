import Mathlib.Algebra.Homology.HomologicalComplexAbelian
import Mathlib.Algebra.Homology.ShortComplex.ShortExact
import Mathlib.RepresentationTheory.Homological.GroupCohomology.Basic
import Mathlib.RepresentationTheory.Invariants
import Mathlib.RepresentationTheory.Homological.GroupCohomology.LowDegree
universe v u
variable (n : â„•)

open CategoryTheory

lemma Fin.comp_contractNth {G H : Type*} [MulOneClass G] [MulOneClass H] (f : G â†’* H)
    (j : Fin (n + 1)) (g : Fin (n + 1) â†’ G) :
    f âˆ˜ Fin.contractNth j (Â· * Â·) g = Fin.contractNth j (Â· * Â·) (f âˆ˜ g) := by
  ext x
  rcases lt_trichotomy (x : â„•) j with (h|h|h)
  Â· simp only [Function.comp_apply, Fin.contractNth_apply_of_lt, h]
  Â· simp only [Function.comp_apply, Fin.contractNth_apply_of_eq, h, f.map_mul]
  Â· simp only [Function.comp_apply, Fin.contractNth_apply_of_gt, h]

namespace LinearMap

lemma ker_compLeft
    {R M N : Type*} [Semiring R] [AddCommMonoid M] [AddCommMonoid N]
    [Module R M] [Module R N] (f : M â†’â‚—[R] N) (I : Type*) :
    LinearMap.ker (f.compLeft I) = Submodule.pi (Set.univ : Set I) (fun _ => LinearMap.ker f) :=
  Submodule.ext fun _ => âŸ¨fun (hx : _ = _) i _ => congr_fun hx i,
    fun hx => funext fun i => hx i trivialâŸ©

lemma range_compLeft
    {R M N : Type*} [Semiring R] [AddCommMonoid M] [AddCommMonoid N]
    [Module R M] [Module R N] (f : M â†’â‚—[R] N) (I : Type*) :
    LinearMap.range (f.compLeft I) = Submodule.pi (Set.univ : Set I)
      (fun _ => LinearMap.range f) :=
  Submodule.ext fun _ => âŸ¨fun âŸ¨y, hyâŸ© i _ => âŸ¨y i, congr_fun hy iâŸ©, fun hx => by
    choose y hy using hx
    exact âŸ¨fun i => y i trivial, funext fun i => hy i trivialâŸ©âŸ©

end LinearMap
namespace ModuleCat

variable (R : Type u) [Ring R]

lemma ofHom_comp {M N P : Type v} [AddCommGroup M] [AddCommGroup N] [AddCommGroup P]
    [Module R M] [Module R N] [Module R P] (f : M â†’â‚—[R] N) (g : N â†’â‚—[R] P) :
    ofHom (g âˆ˜â‚— f) = ofHom f â‰« ofHom g := rfl

end ModuleCat

namespace groupCohomology
open Rep

variable {k G H : Type u} [CommRing k] [Group G] [Group H]
  (A : Rep k G) (B : Rep k H) (f : G â†’* H) (Ï† : B â†’â‚—[k] A) (n : â„•)

class IsPairMap : Prop where
  compatible : âˆ€ (g : G), Ï† âˆ˜â‚— B.Ï (f g) = A.Ï g âˆ˜â‚— Ï†

namespace IsPairMap
open Representation

variable {A B f Ï†} (S : Subgroup G)

lemma compatible_apply [IsPairMap A B f Ï†] (g : G) (x : B) :
    Ï† (B.Ï (f g) x) = A.Ï g (Ï† x) :=
  congr($(compatible g) x)

instance comp {k G H K : Type u} [CommRing k] [Group G] [Group H] [Group K]
    (A : Rep k G) (B : Rep k H) (C : Rep k K) (f : G â†’* H) (g : H â†’* K) (Ï† : B â†’â‚—[k] A)
    (Ïˆ : C â†’â‚—[k] B) [IsPairMap A B f Ï†] [IsPairMap B C g Ïˆ] :
    IsPairMap A C (g.comp f) (Ï†.comp Ïˆ) where
  compatible x := by
    ext y
    have := congr($(compatible (A := A) (B := B) (f := f) (Ï† := Ï†) x) (Ïˆ y))
    have := congr($(compatible (A := B) (B := C) (f := g) (Ï† := Ïˆ) (f x)) y)
    simp_all

instance instInf [S.Normal] : IsPairMap A (inf A S) (QuotientGroup.mk' S)
    (invariants (A.Ï.comp S.subtype)).subtype where
  compatible := by intros; rfl

instance instRes : IsPairMap ((Action.res _ f).obj B) B f LinearMap.id where
  compatible := by intros; rfl

instance instHom {A B : Rep k G} (f : A âŸ¶ B) : IsPairMap B A (MonoidHom.id G) f.hom where
  compatible := f.comm

variable [IsPairMap A B f Ï†]

variable (A B f Ï†) in
@[simps (config := .lemmasOnly)]
noncomputable def cochainsMap :
    inhomogeneousCochains B âŸ¶ inhomogeneousCochains A where
  f i := ModuleCat.ofHom (Ï†.compLeft (Fin i â†’ G)
    âˆ˜â‚— LinearMap.funLeft k B (fun x : Fin i â†’ G => (f âˆ˜ x)))
  comm' i j (hij : _ = _) := by
    subst hij
    ext x
    funext g
    simp only [CochainComplex.of_x, inhomogeneousCochains.d_def, ModuleCat.coe_comp,
      Function.comp_apply]
    simpa [ModuleCat.ofHom, ModuleCat.coe_of, ModuleCat.hom_def, Fin.comp_contractNth]
      using (compatible_apply _ _).symm

@[simp]
lemma cochainsMap_f_apply (n : â„•) (x : (inhomogeneousCochains B).X n) (g : Fin n â†’ G) :
    (cochainsMap A B f Ï†).f n x g = Ï† (x (f âˆ˜ g)) :=
  rfl

@[simp]
lemma cochainsMap_id :
    cochainsMap A A (MonoidHom.id _) (Action.Hom.hom <| ðŸ™ A) = ðŸ™ (inhomogeneousCochains A) := by
  rfl

lemma cochainsMap_comp {k G H K : Type u} [CommRing k] [Group G] [Group H] [Group K]
    (A : Rep k G) (B : Rep k H) (C : Rep k K) (f : G â†’* H) (g : H â†’* K) (Ï† : B â†’â‚—[k] A)
    (Ïˆ : C â†’â‚—[k] B) [IsPairMap A B f Ï†] [IsPairMap B C g Ïˆ] :
    cochainsMap A C (g.comp f) (Ï†.comp Ïˆ) = (cochainsMap B C g Ïˆ) â‰« (cochainsMap A B f Ï†) := by
  rfl

variable (A B f Ï†)
noncomputable abbrev cocyclesMap (n : â„•) :
    groupCohomology.cocycles B n âŸ¶ groupCohomology.cocycles A n :=
  HomologicalComplex.cyclesMap (cochainsMap A B f Ï†) n

noncomputable abbrev cohomologyMap (n : â„•) :
  groupCohomology B n âŸ¶ groupCohomology A n :=
HomologicalComplex.homologyMap (cochainsMap A B f Ï†) n

@[reassoc (attr := simp)]
lemma cochainsMap_f_0_comp_zeroCochainsLEquiv :
    (cochainsMap A B f Ï†).f 0 â‰« (zeroCochainsLEquiv A : (inhomogeneousCochains A).X 0 â†’â‚—[k] A)
      = (zeroCochainsLEquiv B : (inhomogeneousCochains B).X 0 â†’â‚—[k] B) â‰« ModuleCat.ofHom Ï† := by
  ext x
  simp only [cochainsMap_f, Unique.eq_default (f âˆ˜ _)]
  rfl

@[reassoc (attr := simp)]
lemma cochainsMap_f_1_comp_oneCochainsLEquiv :
    (cochainsMap A B f Ï†).f 1 â‰« (oneCochainsLEquiv A : (inhomogeneousCochains A).X 1 â†’â‚—[k] G â†’ A)
      = (oneCochainsLEquiv B).toModuleIso.hom
      â‰« ModuleCat.ofHom (Ï†.compLeft G âˆ˜â‚— LinearMap.funLeft k B f) := by
  ext x
  simp only [cochainsMap_f, Unique.eq_default (f âˆ˜ _)]
  rfl

@[reassoc (attr := simp)]
lemma cochainsMap_f_2_comp_twoCochainsLEquiv :
    (cochainsMap A B f Ï†).f 2
      â‰« (twoCochainsLEquiv A : (inhomogeneousCochains A).X 2 â†’â‚—[k] G Ã— G â†’ A)
      = (twoCochainsLEquiv B).toModuleIso.hom
      â‰« ModuleCat.ofHom (Ï†.compLeft (G Ã— G) âˆ˜â‚— LinearMap.funLeft k B (Prod.map f f)) := by
  ext x
  funext g
  show Ï† (x _) = Ï† (x _)
  rcongr x
  fin_cases x <;> rfl

@[reassoc (attr := simp)]
lemma cochainsMap_f_3_comp_threeCochainsLEquiv :
    (cochainsMap A B f Ï†).f 3
      â‰« (threeCochainsLEquiv A : (inhomogeneousCochains A).X 3 â†’â‚—[k] G Ã— G Ã— G â†’ A)
      = (threeCochainsLEquiv B).toModuleIso.hom
      â‰« ModuleCat.ofHom (Ï†.compLeft (G Ã— G Ã— G)
        âˆ˜â‚— LinearMap.funLeft k B (Prod.map f (Prod.map f f))) := by
  ext x
  funext g
  show Ï† (x _) = Ï† (x _)
  rcongr x
  fin_cases x <;> rfl

open ShortComplex

def mapH0 : H0 B â†’â‚—[k] H0 A :=
  LinearMap.codRestrict _ (Ï† âˆ˜â‚— B.Ï.invariants.subtype) fun âŸ¨c, hcâŸ© g => by
    simp [â† compatible_apply (f := f) g c, hc (f g)]

@[simps]
def mapShortComplexH1 :
    shortComplexH1 B âŸ¶ shortComplexH1 A where
  Ï„â‚ := ModuleCat.ofHom Ï†
  Ï„â‚‚ := ModuleCat.ofHom (Ï†.compLeft G âˆ˜â‚— LinearMap.funLeft k B f)
  Ï„â‚ƒ := ModuleCat.ofHom (Ï†.compLeft (G Ã— G) âˆ˜â‚— LinearMap.funLeft k B (Prod.map f f))
  commâ‚â‚‚ := by
    ext x
    funext g
    dsimp [shortComplexH1, dZero]
    simp [ModuleCat.coe_of, ModuleCat.hom_def, compatible_apply]
  commâ‚‚â‚ƒ := by
    ext x
    funext g
    dsimp [shortComplexH1, dOne]
    simp [ModuleCat.coe_of, ModuleCat.hom_def, compatible_apply]

noncomputable abbrev mapOneCocycles :
    oneCocycles B â†’â‚—[k] oneCocycles A :=
  ShortComplex.cyclesMap' (mapShortComplexH1 A B f Ï†) (shortComplexH1 B).moduleCatLeftHomologyData
    (shortComplexH1 A).moduleCatLeftHomologyData

noncomputable abbrev mapH1 : H1 B â†’â‚—[k] H1 A :=
  ShortComplex.leftHomologyMap' (mapShortComplexH1 A B f Ï†)
    (shortComplexH1 B).moduleCatLeftHomologyData
    (shortComplexH1 A).moduleCatLeftHomologyData

@[simp]
lemma subtype_comp_mapOneCocycles :
    (oneCocycles A).subtype âˆ˜â‚— mapOneCocycles A B f Ï†
      = Ï†.compLeft G âˆ˜â‚— LinearMap.funLeft k B f âˆ˜â‚— (oneCocycles B).subtype :=
  ShortComplex.cyclesMap'_i (mapShortComplexH1 A B f Ï†) (moduleCatLeftHomologyData _)
    (moduleCatLeftHomologyData _)

@[simp]
lemma H1Ï€_comp_mapH1 :
    mapH1 A B f Ï† âˆ˜â‚— H1Ï€ B = H1Ï€ A âˆ˜â‚— mapOneCocycles A B f Ï† :=
  leftHomologyÏ€_naturality' (mapShortComplexH1 A B f Ï†) _ _

@[reassoc (attr := simp)]
lemma cocyclesMap_comp_isoOneCocycles_hom :
    cocyclesMap A B f Ï† 1 â‰« (isoOneCocycles A).hom
      = (isoOneCocycles B).hom â‰« mapOneCocycles A B f Ï† := by
  simp_rw [â† cancel_mono (moduleCatLeftHomologyData (shortComplexH1 A)).i, mapOneCocycles,
      Category.assoc, cyclesMap'_i, isoOneCocycles, â† Category.assoc]
  simp

@[reassoc (attr := simp)]
lemma cohomologyMap_comp_isoH1_hom :
    cohomologyMap A B f Ï† 1 â‰« (isoH1 A).hom = (isoH1 B).hom â‰« mapH1 A B f Ï† := by
  simpa [â† cancel_epi (groupCohomologyÏ€ _ _), mapH1, Category.assoc]
    using (leftHomologyÏ€_naturality' (mapShortComplexH1 A B f Ï†)
    (moduleCatLeftHomologyData _) (moduleCatLeftHomologyData _)).symm

@[simps]
def mapShortComplexH2 :
    shortComplexH2 B âŸ¶ shortComplexH2 A where
  Ï„â‚ := ModuleCat.ofHom (Ï†.compLeft G âˆ˜â‚— LinearMap.funLeft k B f)
  Ï„â‚‚ := ModuleCat.ofHom (Ï†.compLeft (G Ã— G) âˆ˜â‚— LinearMap.funLeft k B (Prod.map f f))
  Ï„â‚ƒ := ModuleCat.ofHom (Ï†.compLeft (G Ã— G Ã— G)
    âˆ˜â‚— LinearMap.funLeft k B (Prod.map f (Prod.map f f)))
  commâ‚â‚‚ := by
    ext x
    funext g
    dsimp [shortComplexH2, dOne]
    simp [ModuleCat.coe_of, ModuleCat.hom_def, compatible_apply]
  commâ‚‚â‚ƒ := by
    ext x
    funext g
    dsimp [shortComplexH2, dTwo]
    simp [ModuleCat.coe_of, ModuleCat.hom_def, compatible_apply]

noncomputable abbrev mapTwoCocycles :
    twoCocycles B â†’â‚—[k] twoCocycles A :=
  ShortComplex.cyclesMap' (mapShortComplexH2 A B f Ï†) (shortComplexH2 B).moduleCatLeftHomologyData
    (shortComplexH2 A).moduleCatLeftHomologyData

noncomputable abbrev mapH2 : H2 B â†’â‚—[k] H2 A :=
  ShortComplex.leftHomologyMap' (mapShortComplexH2 A B f Ï†)
    (shortComplexH2 B).moduleCatLeftHomologyData
    (shortComplexH2 A).moduleCatLeftHomologyData

@[simp]
lemma subtype_comp_mapTwoCocycles :
    (twoCocycles A).subtype âˆ˜â‚— mapTwoCocycles A B f Ï†
      = Ï†.compLeft (G Ã— G) âˆ˜â‚— LinearMap.funLeft k B (Prod.map f f) âˆ˜â‚— (twoCocycles B).subtype :=
  ShortComplex.cyclesMap'_i (mapShortComplexH2 A B f Ï†) (moduleCatLeftHomologyData _)
    (moduleCatLeftHomologyData _)

@[simp]
lemma H2Ï€_comp_mapH2 :
    mapH2 A B f Ï† âˆ˜â‚— H2Ï€ B = H2Ï€ A âˆ˜â‚— mapTwoCocycles A B f Ï† :=
  leftHomologyÏ€_naturality' (mapShortComplexH2 A B f Ï†) _ _

@[reassoc (attr := simp)]
lemma cocyclesMap_comp_isoTwoCocycles_hom :
    cocyclesMap A B f Ï† 2 â‰« (isoTwoCocycles A).hom
      = (isoTwoCocycles B).hom â‰« mapTwoCocycles A B f Ï† := by
  simp_rw [â† cancel_mono (moduleCatLeftHomologyData (shortComplexH2 A)).i, mapTwoCocycles,
      Category.assoc, cyclesMap'_i, isoTwoCocycles, â† Category.assoc]
  simp

@[reassoc (attr := simp)]
lemma cohomologyMap_comp_isoH2_hom :
    cohomologyMap A B f Ï† 2 â‰« (isoH2 A).hom = (isoH2 B).hom â‰« mapH2 A B f Ï† := by
  simpa [â† cancel_epi (groupCohomologyÏ€ _ _), mapH2, Category.assoc]
    using (leftHomologyÏ€_naturality' (mapShortComplexH2 A B f Ï†)
    (moduleCatLeftHomologyData _) (moduleCatLeftHomologyData _)).symm

end IsPairMap

open IsPairMap

variable (k G) in
@[simps]
noncomputable def cochainsFunctor : Rep k G â¥¤ CochainComplex (ModuleCat k) â„• where
  obj A := inhomogeneousCochains A
  map f := cochainsMap _ _ (MonoidHom.id _) f.hom
  map_id _ := cochainsMap_id
  map_comp {X Y Z} Ï† Ïˆ := cochainsMap_comp Z Y X (MonoidHom.id G) (MonoidHom.id G) Ïˆ.hom Ï†.hom

instance : (cochainsFunctor k G).PreservesZeroMorphisms where
instance : (cochainsFunctor k G).Additive where

variable (k G) in
@[simps]
noncomputable def functor (n : â„•) : Rep k G â¥¤ ModuleCat k where
  obj A := groupCohomology A n
  map {A B} Ï† :=
    letI : IsPairMap B A (MonoidHom.id _) Ï†.hom := instHom Ï†
    cohomologyMap B A (MonoidHom.id _) Ï†.hom n
  map_id A := HomologicalComplex.homologyMap_id _ _
  map_comp f g := by
    simp only [â† HomologicalComplex.homologyMap_comp]
    rfl

open ShortComplex

def mapShortExact (X : ShortComplex (Rep k G)) (H : ShortExact X) :
    ShortExact ((cochainsFunctor k G).mapShortComplex.obj X) :=
  HomologicalComplex.shortExact_of_degreewise_shortExact _ fun i => {
    exact := by
      rw [ShortComplex.moduleCat_exact_iff_range_eq_ker]
      have : LinearMap.range X.f.hom = LinearMap.ker X.g.hom :=
        (H.exact.map (forgetâ‚‚ (Rep k G) (ModuleCat k))).moduleCat_range_eq_ker
      show LinearMap.range (LinearMap.compLeft X.f.hom (Fin i â†’ G))
        = LinearMap.ker (LinearMap.compLeft X.g.hom (Fin i â†’ G))
      rw [LinearMap.range_compLeft, LinearMap.ker_compLeft, this]
    mono_f := letI := H.2
      (ModuleCat.mono_iff_injective _).2 <| Function.Injective.comp_left <|
        (ModuleCat.mono_iff_injective X.f.hom).1 <| (forgetâ‚‚ (Rep k G) (ModuleCat k)).map_mono X.f
    epi_g := letI := H.3
      (ModuleCat.epi_iff_surjective _).2 <| Function.Surjective.comp_left <|
        (ModuleCat.epi_iff_surjective X.g.hom).1 <| (forgetâ‚‚ (Rep k G) (ModuleCat k)).map_epi X.g }

end groupCohomology
