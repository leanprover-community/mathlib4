/-
Copyright (c) 2025 Amelia Livingston. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Amelia Livingston
-/
import Mathlib.RepresentationTheory.Homological.GroupCohomology.Basic
import Mathlib.RepresentationTheory.Homological.GroupCohomology.LowDegree

/-!
# Functoriality of group cohomology

Given a commutative ring `k`, a group homomorphism `f : G â†’* H`, a `k`-linear `H`-representation
`A`, a `k`-linear `G`-representation `B`, and a representation morphism `Res(f)(A) âŸ¶ B`, we get
a cochain map `inhomogeneousCochains A âŸ¶ inhomogeneousCochains B` and hence maps on
cohomology `Hâ¿(H, A) âŸ¶ Hâ¿(G, B)`.
We also provide extra API for these maps in degrees 0, 1, 2.

## Main definitions

* `groupCohomology.cochainsMap f Ï†` is the map `inhomogeneousCochains A âŸ¶ inhomogeneousCochains B`
  induced by a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : Res(f)(A) âŸ¶ B`.
* `groupCohomology.map f Ï† n` is the map `Hâ¿(H, A) âŸ¶ Hâ¿(G, B)` induced by a group
  homomorphism `f : G â†’* H` and a representation morphism `Ï† : Res(f)(A) âŸ¶ B`.
* `groupCohomology.H1InfRes A S` is the short complex `HÂ¹(G â§¸ S, A^S) âŸ¶ HÂ¹(G, A) âŸ¶ HÂ¹(S, A)` for
  a normal subgroup `S â‰¤ G` and a `G`-representation `A`.

-/

universe v u

namespace groupCohomology
open Rep CategoryTheory Representation

variable {k G H : Type u} [CommRing k] [Group G] [Group H]
  {A : Rep k H} {B : Rep k G} (f : G â†’* H) (Ï† : (Action.res _ f).obj A âŸ¶ B) (n : â„•)

section

theorem congr {fâ‚ fâ‚‚ : G â†’* H} (h : fâ‚ = fâ‚‚) {Ï† : (Action.res _ fâ‚).obj A âŸ¶ B} {T : Type*}
    (F : (f : G â†’* H) â†’ (Ï† : (Action.res _ f).obj A âŸ¶ B) â†’ T) :
    F fâ‚ Ï† = F fâ‚‚ (h â–¸ Ï†) := by
  subst h
  rfl

variable [DecidableEq G] [DecidableEq H]

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : Res(f)(A) âŸ¶ B`,
this is the chain map sending `x : Hâ¿ â†’ A` to `(g : Gâ¿) â†¦ Ï† (x (f âˆ˜ g))`. -/
@[simps! -isSimp f f_hom]
noncomputable def cochainsMap :
    inhomogeneousCochains A âŸ¶ inhomogeneousCochains B where
  f i := ModuleCat.ofHom <|
    Ï†.hom.hom.compLeft (Fin i â†’ G) âˆ˜â‚— LinearMap.funLeft k A (fun x : Fin i â†’ G => (f âˆ˜ x))
  comm' i j (hij : _ = _) := by
    subst hij
    ext
    simpa [inhomogeneousCochains.d_hom_apply, Fin.comp_contractNth]
      using (hom_comm_apply Ï† _ _).symm

@[simp]
lemma cochainsMap_id :
    cochainsMap (MonoidHom.id _) (ğŸ™ A) = ğŸ™ (inhomogeneousCochains A) := by
  rfl

@[simp]
lemma cochainsMap_id_f_hom_eq_compLeft {A B : Rep k G} (f : A âŸ¶ B) (i : â„•) :
    ((cochainsMap (MonoidHom.id G) f).f i).hom = f.hom.hom.compLeft _ := by
  ext
  rfl

@[deprecated (since := "2025-06-11")]
alias cochainsMap_id_f_eq_compLeft := cochainsMap_id_f_hom_eq_compLeft

@[reassoc]
lemma cochainsMap_comp {G H K : Type u} [Group G] [DecidableEq G] [Group H] [DecidableEq H]
    [Group K] [DecidableEq K] {A : Rep k K} {B : Rep k H} {C : Rep k G} (f : H â†’* K) (g : G â†’* H)
    (Ï† : (Action.res _ f).obj A âŸ¶ B) (Ïˆ : (Action.res _ g).obj B âŸ¶ C) :
    cochainsMap (f.comp g) ((Action.res _ g).map Ï† â‰« Ïˆ) =
      cochainsMap f Ï† â‰« cochainsMap g Ïˆ := by
  rfl

@[reassoc]
lemma cochainsMap_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) :
    cochainsMap (MonoidHom.id G) (Ï† â‰« Ïˆ) =
      cochainsMap (MonoidHom.id G) Ï† â‰« cochainsMap (MonoidHom.id G) Ïˆ := by
  rfl

@[simp]
lemma cochainsMap_zero : cochainsMap (A := A) (B := B) f 0 = 0 := by rfl

lemma cochainsMap_f_map_mono (hf : Function.Surjective f) [Mono Ï†] (i : â„•) :
    Mono ((cochainsMap f Ï†).f i) := by
  simpa [ModuleCat.mono_iff_injective] using
    ((Rep.mono_iff_injective Ï†).1 inferInstance).comp_left.comp <|
    LinearMap.funLeft_injective_of_surjective k A _ hf.comp_left

instance cochainsMap_id_f_map_mono {A B : Rep k G} (Ï† : A âŸ¶ B) [Mono Ï†] (i : â„•) :
    Mono ((cochainsMap (MonoidHom.id G) Ï†).f i) :=
  cochainsMap_f_map_mono (MonoidHom.id G) Ï† (fun x => âŸ¨x, rflâŸ©) i

lemma cochainsMap_f_map_epi (hf : Function.Injective f) [Epi Ï†] (i : â„•) :
    Epi ((cochainsMap f Ï†).f i) := by
  simpa [ModuleCat.epi_iff_surjective] using
    ((Rep.epi_iff_surjective Ï†).1 inferInstance).comp_left.comp <|
    LinearMap.funLeft_surjective_of_injective k A _ hf.comp_left

instance cochainsMap_id_f_map_epi {A B : Rep k G} (Ï† : A âŸ¶ B) [Epi Ï†] (i : â„•) :
    Epi ((cochainsMap (MonoidHom.id G) Ï†).f i) :=
  cochainsMap_f_map_epi (MonoidHom.id G) Ï† (fun _ _ h => h) i

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : Res(f)(A) âŸ¶ B`,
this is the induced map `Zâ¿(H, A) âŸ¶ Zâ¿(G, B)` sending `x : Hâ¿ â†’ A` to
`(g : Gâ¿) â†¦ Ï† (x (f âˆ˜ g))`. -/
noncomputable abbrev cocyclesMap (n : â„•) :
    groupCohomology.cocycles A n âŸ¶ groupCohomology.cocycles B n :=
  HomologicalComplex.cyclesMap (cochainsMap f Ï†) n

@[simp]
lemma cocyclesMap_id : cocyclesMap (MonoidHom.id G) (ğŸ™ B) n = ğŸ™ _ :=
  HomologicalComplex.cyclesMap_id _ _

@[reassoc]
lemma cocyclesMap_comp {G H K : Type u} [Group G] [DecidableEq G] [Group H] [DecidableEq H]
    [Group K] [DecidableEq K] {A : Rep k K} {B : Rep k H} {C : Rep k G} (f : H â†’* K) (g : G â†’* H)
    (Ï† : (Action.res _ f).obj A âŸ¶ B) (Ïˆ : (Action.res _ g).obj B âŸ¶ C) (n : â„•) :
    cocyclesMap (f.comp g) ((Action.res _ g).map Ï† â‰« Ïˆ) n =
      cocyclesMap f Ï† n â‰« cocyclesMap g Ïˆ n := by
  simp [cocyclesMap, â† HomologicalComplex.cyclesMap_comp, â† cochainsMap_comp]

@[reassoc]
theorem cocyclesMap_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) (n : â„•) :
    cocyclesMap (MonoidHom.id G) (Ï† â‰« Ïˆ) n =
      cocyclesMap (MonoidHom.id G) Ï† n â‰« cocyclesMap (MonoidHom.id G) Ïˆ n := by
  simp [cocyclesMap, cochainsMap_id_comp, HomologicalComplex.cyclesMap_comp]

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : Res(f)(A) âŸ¶ B`,
this is the induced map `Hâ¿(H, A) âŸ¶ Hâ¿(G, B)` sending `x : Hâ¿ â†’ A` to
`(g : Gâ¿) â†¦ Ï† (x (f âˆ˜ g))`. -/
noncomputable abbrev map (n : â„•) :
    groupCohomology A n âŸ¶ groupCohomology B n :=
  HomologicalComplex.homologyMap (cochainsMap f Ï†) n

@[reassoc, elementwise]
theorem Ï€_map (n : â„•) :
    Ï€ A n â‰« map f Ï† n = cocyclesMap f Ï† n â‰« Ï€ B n := by
  simp [map, cocyclesMap]

@[simp]
lemma map_id : map (MonoidHom.id G) (ğŸ™ B) n = ğŸ™ _ := HomologicalComplex.homologyMap_id _ _

@[reassoc]
lemma map_comp {G H K : Type u} [Group G] [DecidableEq G] [Group H] [DecidableEq H]
    [Group K] [DecidableEq K] {A : Rep k K} {B : Rep k H} {C : Rep k G} (f : H â†’* K) (g : G â†’* H)
    (Ï† : (Action.res _ f).obj A âŸ¶ B) (Ïˆ : (Action.res _ g).obj B âŸ¶ C) (n : â„•) :
    map (f.comp g) ((Action.res _ g).map Ï† â‰« Ïˆ) n = map f Ï† n â‰« map g Ïˆ n := by
  simp [map, â† HomologicalComplex.homologyMap_comp, â† cochainsMap_comp]

@[reassoc]
theorem map_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) (n : â„•) :
    map (MonoidHom.id G) (Ï† â‰« Ïˆ) n =
      map (MonoidHom.id G) Ï† n â‰« map (MonoidHom.id G) Ïˆ n := by
  rw [map, cochainsMap_id_comp, HomologicalComplex.homologyMap_comp]

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : Res(f)(A) âŸ¶ B`,
this is the induced map sending `x : H â†’ A` to `(g : G) â†¦ Ï† (x (f g))`. -/
noncomputable abbrev fOne :
    ModuleCat.of k (H â†’ A) âŸ¶ ModuleCat.of k (G â†’ B) :=
  ModuleCat.ofHom <| Ï†.hom.hom.compLeft G âˆ˜â‚— LinearMap.funLeft k A f

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : Res(f)(A) âŸ¶ B`,
this is the induced map sending `x : H Ã— H â†’ A` to `(gâ‚, gâ‚‚ : G Ã— G) â†¦ Ï† (x (f gâ‚, f gâ‚‚))`. -/
noncomputable abbrev fTwo :
    ModuleCat.of k (H Ã— H â†’ A) âŸ¶ ModuleCat.of k (G Ã— G â†’ B) :=
  ModuleCat.ofHom <| Ï†.hom.hom.compLeft (G Ã— G) âˆ˜â‚— LinearMap.funLeft k A (Prod.map f f)

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : Res(f)(A) âŸ¶ B`,
this is the induced map sending `x : H Ã— H Ã— H â†’ A` to
`(gâ‚, gâ‚‚, gâ‚ƒ : G Ã— G Ã— G) â†¦ Ï† (x (f gâ‚, f gâ‚‚, f gâ‚ƒ))`. -/
noncomputable abbrev fThree :
    ModuleCat.of k (H Ã— H Ã— H â†’ A) âŸ¶ ModuleCat.of k (G Ã— G Ã— G â†’ B) :=
  ModuleCat.ofHom <|
    Ï†.hom.hom.compLeft (G Ã— G Ã— G) âˆ˜â‚— LinearMap.funLeft k A (Prod.map f (Prod.map f f))

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma cochainsMap_f_0_comp_zeroCochainsIso :
    (cochainsMap f Ï†).f 0 â‰« (zeroCochainsIso B).hom = (zeroCochainsIso A).hom â‰« Ï†.hom := by
  ext x
  simp only [cochainsMap_f, Unique.eq_default (f âˆ˜ _)]
  rfl

@[deprecated (since := "2025-05-09")]
alias cochainsMap_f_0_comp_zeroCochainsLequiv := cochainsMap_f_0_comp_zeroCochainsIso

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma cochainsMap_f_1_comp_oneCochainsIso :
    (cochainsMap f Ï†).f 1 â‰« (oneCochainsIso B).hom = (oneCochainsIso A).hom â‰« fOne f Ï† := by
  ext x
  simp only [cochainsMap_f, Unique.eq_default (f âˆ˜ _)]
  rfl

@[deprecated (since := "2025-05-09")]
alias cochainsMap_f_1_comp_oneCochainsLequiv := cochainsMap_f_1_comp_oneCochainsIso

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma cochainsMap_f_2_comp_twoCochainsIso :
    (cochainsMap f Ï†).f 2 â‰« (twoCochainsIso B).hom = (twoCochainsIso A).hom â‰« fTwo f Ï† := by
  ext x g
  show Ï†.hom (x _) = Ï†.hom (x _)
  rcongr x
  fin_cases x <;> rfl

@[deprecated (since := "2025-05-09")]
alias cochainsMap_f_2_comp_twoCochainsLequiv := cochainsMap_f_2_comp_twoCochainsIso

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma cochainsMap_f_3_comp_threeCochainsIso :
    (cochainsMap f Ï†).f 3 â‰« (threeCochainsIso B).hom = (threeCochainsIso A).hom â‰« fThree f Ï† := by
  ext x g
  show Ï†.hom (x _) = Ï†.hom (x _)
  rcongr x
  fin_cases x <;> rfl

@[deprecated (since := "2025-05-09")]
alias cochainsMap_f_3_comp_threeCochainsLequiv := cochainsMap_f_3_comp_threeCochainsIso

end

open ShortComplex

section H0

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : Res(f)(A) âŸ¶ B`,
this is induced map `Aá´´ âŸ¶ Bá´³`. -/
@[deprecated (since := "2025-06-09")]
alias H0Map := map

@[deprecated (since := "2025-06-09")]
alias H0Map_id := map_id

@[deprecated (since := "2025-06-09")]
alias H0Map_comp := map_comp

@[deprecated (since := "2025-06-09")]
alias H0Map_id_comp := map_id_comp

variable [DecidableEq G] [DecidableEq H]

@[reassoc (attr := simp), elementwise (attr := simp)]
theorem map_H0Iso_hom_f :
    map f Ï† 0 â‰« (H0Iso B).hom â‰« (shortComplexH0 B).f =
      (H0Iso A).hom â‰« (shortComplexH0 A).f â‰« Ï†.hom := by
  simp [â† cancel_epi (Ï€ _ _)]

@[deprecated (since := "2025-06-09")]
alias H0Map_comp_f := map_H0Iso_hom_f

@[reassoc (attr := simp), elementwise (attr := simp)]
theorem map_id_comp_H0Iso_hom {A B : Rep k G} (f : A âŸ¶ B) :
    map (MonoidHom.id G) f 0 â‰« (H0Iso B).hom = (H0Iso A).hom â‰« (invariantsFunctor k G).map f := by
  simp only [â† cancel_mono (shortComplexH0 B).f, Category.assoc, map_H0Iso_hom_f]
  rfl

@[deprecated (since := "2025-06-09")]
alias H0Map_id_eq_invariantsFunctor_map := map_id_comp_H0Iso_hom

instance mono_map_0_of_mono {A B : Rep k G} (f : A âŸ¶ B) [Mono f] :
    Mono (map (MonoidHom.id G) f 0) where
  right_cancellation g h hgh := by
    simp only [â† cancel_mono (H0Iso B).hom, Category.assoc, map_id_comp_H0Iso_hom] at hgh
    simp_all [cancel_mono]

@[deprecated (since := "2025-06-09")]
alias mono_H0Map_of_mono := mono_map_0_of_mono

@[reassoc, elementwise]
theorem cocyclesMap_zeroIsoCocycles_hom_f :
    cocyclesMap f Ï† 0 â‰« (zeroCocyclesIso B).hom â‰« (shortComplexH0 B).f =
      (zeroCocyclesIso A).hom â‰« (shortComplexH0 A).f â‰« Ï†.hom := by
  simp

@[deprecated (since := "2025-06-12")]
alias cocyclesMap_comp_isoZeroCocycles_hom := cocyclesMap_zeroIsoCocycles_hom_f

end H0
section H1

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : Res(f)(A) âŸ¶ B`,
this is the induced map from the short complex `A --dZero--> Fun(H, A) --dOne--> Fun(H Ã— H, A)`
to `B --dZero--> Fun(G, B) --dOne--> Fun(G Ã— G, B)`. -/
@[simps]
noncomputable def mapShortComplexH1 :
    shortComplexH1 A âŸ¶ shortComplexH1 B where
  Ï„â‚ := Ï†.hom
  Ï„â‚‚ := fOne f Ï†
  Ï„â‚ƒ := fTwo f Ï†
  commâ‚â‚‚ := by
    ext x
    funext g
    simpa [shortComplexH1, dZero, fOne] using (hom_comm_apply Ï† g x).symm
  commâ‚‚â‚ƒ := by
    ext x
    funext g
    simpa [shortComplexH1, dOne, fOne, fTwo] using (hom_comm_apply Ï† _ _).symm

@[simp]
theorem mapShortComplexH1_zero :
    mapShortComplexH1 (A := A) (B := B) f 0 = 0 := by
  rfl

@[simp]
theorem mapShortComplexH1_id :
    mapShortComplexH1 (MonoidHom.id _) (ğŸ™ A) = ğŸ™ _ := by
  rfl

@[reassoc]
theorem mapShortComplexH1_comp {G H K : Type u} [Group G] [Group H] [Group K]
    {A : Rep k K} {B : Rep k H} {C : Rep k G} (f : H â†’* K) (g : G â†’* H)
    (Ï† : (Action.res _ f).obj A âŸ¶ B) (Ïˆ : (Action.res _ g).obj B âŸ¶ C) :
    mapShortComplexH1 (f.comp g) ((Action.res _ g).map Ï† â‰« Ïˆ) =
      mapShortComplexH1 f Ï† â‰« mapShortComplexH1 g Ïˆ := rfl

@[reassoc]
theorem mapShortComplexH1_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) :
    mapShortComplexH1 (MonoidHom.id G) (Ï† â‰« Ïˆ) =
      mapShortComplexH1 (MonoidHom.id G) Ï† â‰« mapShortComplexH1 (MonoidHom.id G) Ïˆ := rfl

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : Res(f)(A) âŸ¶ B`,
this is induced map `ZÂ¹(H, A) âŸ¶ ZÂ¹(G, B)`. -/
noncomputable abbrev mapOneCocycles :
    ModuleCat.of k (oneCocycles A) âŸ¶ ModuleCat.of k (oneCocycles B) :=
  ShortComplex.cyclesMap' (mapShortComplexH1 f Ï†) (shortComplexH1 A).moduleCatLeftHomologyData
    (shortComplexH1 B).moduleCatLeftHomologyData

@[reassoc, elementwise]
lemma mapOneCocycles_comp_i :
    mapOneCocycles f Ï† â‰« (shortComplexH1 B).moduleCatLeftHomologyData.i =
      (shortComplexH1 A).moduleCatLeftHomologyData.i â‰« fOne f Ï† := by
  simp

@[simp]
lemma coe_mapOneCocycles (x) :
    â‡‘(mapOneCocycles f Ï† x) = fOne f Ï† x := rfl

@[deprecated (since := "2025-05-09")]
alias mapOneCocycles_comp_subtype := mapOneCocycles_comp_i

variable [DecidableEq G] [DecidableEq H] in
@[reassoc (attr := simp), elementwise (attr := simp)]
lemma cocyclesMap_comp_isoOneCocycles_hom :
    cocyclesMap f Ï† 1 â‰« (isoOneCocycles B).hom = (isoOneCocycles A).hom â‰« mapOneCocycles f Ï† := by
  simp [â† cancel_mono (moduleCatLeftHomologyData (shortComplexH1 B)).i, mapShortComplexH1,
    cochainsMap_f_1_comp_oneCochainsIso f]

@[simp]
theorem mapOneCocycles_one (Ï† : (Action.res _ 1).obj A âŸ¶ B) :
    mapOneCocycles 1 Ï† = 0 := by
  rw [â† cancel_mono (moduleCatLeftHomologyData (shortComplexH1 B)).i, cyclesMap'_i]
  refine ModuleCat.hom_ext (LinearMap.ext fun _ â†¦ funext fun y => ?_)
  simp [mapShortComplexH1, shortComplexH1, moduleCatMk, Pi.zero_apply y]

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : Res(f)(A) âŸ¶ B`,
this is induced map `HÂ¹(H, A) âŸ¶ HÂ¹(G, B)`. -/
@[deprecated (since := "2025-06-09")]
alias H1Map := map

@[deprecated (since := "2025-6-09")]
alias H1Map_id := map_id

@[deprecated (since := "2025-06-09")]
alias H1Map_comp := map_comp

@[deprecated (since := "2025-06-09")]
alias H1Map_id_comp := map_id_comp

variable [DecidableEq G] [DecidableEq H]

@[reassoc (attr := simp), elementwise (attr := simp)]
lemma H1Ï€_comp_map :
    H1Ï€ A â‰« map f Ï† 1 = mapOneCocycles f Ï† â‰« H1Ï€ B := by
  simp [H1Ï€, Iso.inv_comp_eq, â† cocyclesMap_comp_isoOneCocycles_hom_assoc]

@[deprecated (since := "2025-06-12")]
alias H1Ï€_comp_H1Map := H1Ï€_comp_map

@[simp]
theorem map_1_one (Ï† : (Action.res _ 1).obj A âŸ¶ B) :
    map 1 Ï† 1 = 0 := by
  simp [â† cancel_epi (H1Ï€ _)]

@[deprecated (since := "2025-06-09")]
alias H1Map_one := map_1_one

section InfRes

variable (A : Rep k G) (S : Subgroup G) [S.Normal] [DecidableEq (G â§¸ S)]

/-- The short complex `HÂ¹(G â§¸ S, A^S) âŸ¶ HÂ¹(G, A) âŸ¶ HÂ¹(S, A)`. -/
@[simps Xâ‚ Xâ‚‚ Xâ‚ƒ f g]
noncomputable def H1InfRes :
    ShortComplex (ModuleCat k) where
  Xâ‚ := groupCohomology (A.quotientToInvariants S) 1
  Xâ‚‚ := groupCohomology A 1
  Xâ‚ƒ := groupCohomology ((Action.res _ S.subtype).obj A) 1
  f := map (QuotientGroup.mk' S) (subtype _ _ <| le_comap_invariants A.Ï S) 1
  g := map S.subtype (ğŸ™ _) 1
  zero := by rw [â† map_comp, Category.comp_id, congr (QuotientGroup.mk'_comp_subtype S)
    (fun f Ï† => map f Ï† 1), map_1_one]

/-- The inflation map `HÂ¹(G â§¸ S, A^S) âŸ¶ HÂ¹(G, A)` is a monomorphism. -/
instance : Mono (H1InfRes A S).f := by
  rw [ModuleCat.mono_iff_injective, injective_iff_map_eq_zero]
  intro x hx
  induction x using H1_induction_on with | @h x =>
  simp_all only [H1InfRes_Xâ‚‚, H1InfRes_Xâ‚, H1InfRes_f, H1Ï€_comp_map_apply (QuotientGroup.mk' S)]
  rcases (H1Ï€_eq_zero_iff _).1 hx with âŸ¨y, hyâŸ©
  refine (H1Ï€_eq_zero_iff _).2 âŸ¨âŸ¨y, fun s => ?_âŸ©, funext fun g => QuotientGroup.induction_on g
    fun g => Subtype.ext <| by simpa [-SetLike.coe_eq_coe] using congr_fun hy gâŸ©
  simpa [coe_mapOneCocycles (x := x), sub_eq_zero, (QuotientGroup.eq_one_iff s.1).2 s.2] using
    congr_fun hy s.1

/-- Given a `G`-representation `A` and a normal subgroup `S â‰¤ G`, the short complex
`HÂ¹(G â§¸ S, A^S) âŸ¶ HÂ¹(G, A) âŸ¶ HÂ¹(S, A)` is exact. -/
lemma H1InfRes_exact : (H1InfRes A S).Exact := by
  rw [moduleCat_exact_iff_ker_sub_range]
  intro x hx
  induction x using H1_induction_on with | @h x =>
  simp_all only [H1InfRes_Xâ‚‚, H1InfRes_Xâ‚ƒ, H1InfRes_g, H1InfRes_Xâ‚, LinearMap.mem_ker,
    H1Ï€_comp_map_apply S.subtype, H1InfRes_f]
  rcases (H1Ï€_eq_zero_iff _).1 hx with âŸ¨(y : A), hyâŸ©
  have h1 := (mem_oneCocycles_iff x).1 x.2
  have h2 : âˆ€ s âˆˆ S, x s = A.Ï s y - y :=
    fun s hs => funext_iff.1 hy.symm âŸ¨s, hsâŸ©
  refine âŸ¨H1Ï€ _ âŸ¨fun g => Quotient.liftOn' g (fun g => âŸ¨x.1 g - A.Ï g y + y, ?_âŸ©) ?_, ?_âŸ©, ?_âŸ©
  Â· intro s
    calc
      _ = x (s * g) - x s - A.Ï s (A.Ï g y) + (x s + y) := by
        simp [add_eq_of_eq_sub (h2 s s.2), sub_eq_of_eq_add (h1 s g)]
      _ = x (g * (gâ»Â¹ * s * g)) - A.Ï g (A.Ï (gâ»Â¹ * s * g) y - y) - A.Ï g y + y := by
        simp only [mul_assoc, mul_inv_cancel_left, map_mul, Module.End.mul_apply, map_sub,
          Representation.self_inv_apply]
        abel
      _ = x g - A.Ï g y + y := by
        simp [eq_sub_of_add_eq' (h1 g (gâ»Â¹ * s * g)).symm,
          h2 (gâ»Â¹ * s * g) (Subgroup.Normal.conj_mem' â€¹_â€º _ s.2 _)]
  Â· intro g h hgh
    have := congr(A.Ï g $(h2 (gâ»Â¹ * h) <| QuotientGroup.leftRel_apply.1 hgh))
    simp_all [â† sub_eq_add_neg, sub_eq_sub_iff_sub_eq_sub]
  Â· rw [mem_oneCocycles_iff]
    intro g h
    induction g using QuotientGroup.induction_on with | @H g =>
    induction h using QuotientGroup.induction_on with | @H h =>
    apply Subtype.ext
    simp [â† QuotientGroup.mk_mul, h1 g h, sub_add_eq_add_sub, add_assoc]
  Â· symm
    simp only [H1Ï€_comp_map_apply, H1Ï€_eq_iff (A := A)]
    use y
    ext g
    simp [coe_mapOneCocycles (QuotientGroup.mk' S),
      oneCocycles.coe_mk (A := A.quotientToInvariants S), â† sub_sub]

end InfRes
end H1
section H2

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : Res(f)(A) âŸ¶ B`,
this is the induced map from the short complex
`Fun(H, A) --dOne--> Fun(H Ã— H, A) --dTwo--> Fun(H Ã— H Ã— H, A)` to
`Fun(G, B) --dOne--> Fun(G Ã— G, B) --dTwo--> Fun(G Ã— G Ã— G, B)`. -/
@[simps]
noncomputable def mapShortComplexH2 :
    shortComplexH2 A âŸ¶ shortComplexH2 B where
  Ï„â‚ := fOne f Ï†
  Ï„â‚‚ := fTwo f Ï†
  Ï„â‚ƒ := fThree f Ï†
  commâ‚â‚‚ := by
    ext x
    funext g
    simpa [shortComplexH2, dOne, fOne, fTwo] using (hom_comm_apply Ï† _ _).symm
  commâ‚‚â‚ƒ := by
    ext x
    funext g
    simpa [shortComplexH2, dTwo, fTwo, fThree] using (hom_comm_apply Ï† _ _).symm

@[simp]
theorem mapShortComplexH2_zero :
    mapShortComplexH2 (A := A) (B := B) f 0 = 0 := rfl

@[simp]
theorem mapShortComplexH2_id :
    mapShortComplexH2 (MonoidHom.id _) (ğŸ™ A) = ğŸ™ _ := by
  rfl

@[reassoc]
theorem mapShortComplexH2_comp {G H K : Type u} [Group G] [Group H] [Group K]
    {A : Rep k K} {B : Rep k H} {C : Rep k G} (f : H â†’* K) (g : G â†’* H)
    (Ï† : (Action.res _ f).obj A âŸ¶ B) (Ïˆ : (Action.res _ g).obj B âŸ¶ C) :
    mapShortComplexH2 (f.comp g) ((Action.res _ g).map Ï† â‰« Ïˆ) =
      mapShortComplexH2 f Ï† â‰« mapShortComplexH2 g Ïˆ := rfl

@[reassoc]
theorem mapShortComplexH2_id_comp {A B C : Rep k G} (Ï† : A âŸ¶ B) (Ïˆ : B âŸ¶ C) :
    mapShortComplexH2 (MonoidHom.id G) (Ï† â‰« Ïˆ) =
      mapShortComplexH2 (MonoidHom.id G) Ï† â‰« mapShortComplexH2 (MonoidHom.id G) Ïˆ := rfl

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : Res(f)(A) âŸ¶ B`,
this is induced map `ZÂ²(H, A) âŸ¶ ZÂ²(G, B)`. -/
noncomputable abbrev mapTwoCocycles :
    ModuleCat.of k (twoCocycles A) âŸ¶ ModuleCat.of k (twoCocycles B) :=
  ShortComplex.cyclesMap' (mapShortComplexH2 f Ï†) (shortComplexH2 A).moduleCatLeftHomologyData
    (shortComplexH2 B).moduleCatLeftHomologyData

@[reassoc, elementwise]
lemma mapTwoCocycles_comp_i :
    mapTwoCocycles f Ï† â‰« (shortComplexH2 B).moduleCatLeftHomologyData.i =
      (shortComplexH2 A).moduleCatLeftHomologyData.i â‰« fTwo f Ï† := by
  simp

@[simp]
lemma coe_mapTwoCocycles (x) :
    â‡‘(mapTwoCocycles f Ï† x) = fTwo f Ï† x := rfl

@[deprecated (since := "2025-05-09")]
alias mapTwoCocycles_comp_subtype := mapTwoCocycles_comp_i

variable [DecidableEq G] [DecidableEq H] in
@[reassoc (attr := simp), elementwise (attr := simp)]
lemma cocyclesMap_comp_isoTwoCocycles_hom :
    cocyclesMap f Ï† 2 â‰« (isoTwoCocycles B).hom = (isoTwoCocycles A).hom â‰« mapTwoCocycles f Ï† := by
  simp [â† cancel_mono (moduleCatLeftHomologyData (shortComplexH2 B)).i, mapShortComplexH2,
    cochainsMap_f_2_comp_twoCochainsIso f]

/-- Given a group homomorphism `f : G â†’* H` and a representation morphism `Ï† : Res(f)(A) âŸ¶ B`,
this is induced map `HÂ²(H, A) âŸ¶ HÂ²(G, B)`. -/
@[deprecated (since := "2025-06-09")]
alias H2Map := map

@[deprecated (since := "2025-06-09")]
alias H2Map_id := map_id

@[deprecated (since := "2025-06-09")]
alias H2Map_comp := map_comp

@[deprecated (since := "2025-06-09")]
alias H2Map_id_comp := map_id_comp

variable [DecidableEq G] [DecidableEq H] in
@[reassoc (attr := simp), elementwise (attr := simp)]
lemma H2Ï€_comp_map :
    H2Ï€ A â‰« map f Ï† 2 = mapTwoCocycles f Ï† â‰« H2Ï€ B := by
  simp [H2Ï€, Iso.inv_comp_eq, â† cocyclesMap_comp_isoTwoCocycles_hom_assoc]

@[deprecated (since := "2025-06-12")]
alias H2Ï€_comp_H2Map := H2Ï€_comp_map

end H2

variable [DecidableEq G]

variable (k G) in
/-- The functor sending a representation to its complex of inhomogeneous cochains. -/
@[simps]
noncomputable def cochainsFunctor : Rep k G â¥¤ CochainComplex (ModuleCat k) â„• where
  obj A := inhomogeneousCochains A
  map f := cochainsMap (MonoidHom.id _) f
  map_id _ := cochainsMap_id
  map_comp Ï† Ïˆ := cochainsMap_comp (MonoidHom.id G) (MonoidHom.id G) Ï† Ïˆ

instance : (cochainsFunctor k G).PreservesZeroMorphisms where
instance : (cochainsFunctor k G).Additive where

variable (k G) in
/-- The functor sending a `G`-representation `A` to `Hâ¿(G, A)`. -/
@[simps]
noncomputable def functor (n : â„•) : Rep k G â¥¤ ModuleCat k where
  obj A := groupCohomology A n
  map Ï† := map (MonoidHom.id _) Ï† n
  map_id _ := HomologicalComplex.homologyMap_id _ _
  map_comp _ _ := by
    simp only [â† HomologicalComplex.homologyMap_comp]
    rfl

instance (n : â„•) : (functor k G n).PreservesZeroMorphisms where
  map_zero _ _ := by simp [map]

end groupCohomology
