/-
Copyright (c) 2019 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Algebra.Group.TypeTags
import Mathlib.Algebra.Hom.Equiv.Basic
import Mathlib.Algebra.Hom.Ring
import Mathlib.Algebra.Hom.Units

#align_import deprecated.group from "leanprover-community/mathlib"@"5a3e819569b0f12cbec59d740a2613018e7b8eec"

/-!
# Unbundled monoid and group homomorphisms

This file is deprecated, and is no longer imported by anything in mathlib other than other
deprecated files, and test files. You should not need to import it.

This file defines predicates for unbundled monoid and group homomorphisms. Instead of using
this file, please use `MonoidHom`, defined in `Algebra.Hom.Group`, with notation `‚Üí*`, for
morphisms between monoids or groups. For example use `œÜ : G ‚Üí* H` to represent a group
homomorphism between multiplicative groups, and `œà : A ‚Üí+ B` to represent a group homomorphism
between additive groups.

## Main Definitions

`IsMonoidHom` (deprecated), `IsGroupHom` (deprecated)

## Tags

IsGroupHom, IsMonoidHom

-/


universe u v

variable {Œ± : Type u} {Œ≤ : Type v}

/-- Predicate for maps which preserve an addition. -/
structure IsAddHom {Œ± Œ≤ : Type*} [Add Œ±] [Add Œ≤] (f : Œ± ‚Üí Œ≤) : Prop where
  /-- The proposition that `f` preserves addition. -/
  map_add : ‚àÄ x y, f (x + y) = f x + f y
#align is_add_hom IsAddHom

/-- Predicate for maps which preserve a multiplication. -/
@[to_additive]
structure IsMulHom {Œ± Œ≤ : Type*} [Mul Œ±] [Mul Œ≤] (f : Œ± ‚Üí Œ≤) : Prop where
  /-- The proposition that `f` preserves multiplication. -/
  map_mul : ‚àÄ x y, f (x * y) = f x * f y
#align is_mul_hom IsMulHom

namespace IsMulHom

variable [Mul Œ±] [Mul Œ≤] {Œ≥ : Type*} [Mul Œ≥]

/-- The identity map preserves multiplication. -/
@[to_additive "The identity map preserves addition"]
theorem id : IsMulHom (id : Œ± ‚Üí Œ±) :=
  { map_mul := fun _ _ => rfl }
#align is_mul_hom.id IsMulHom.id
#align is_add_hom.id IsAddHom.id

/-- The composition of maps which preserve multiplication, also preserves multiplication. -/
@[to_additive "The composition of addition preserving maps also preserves addition"]
theorem comp {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ≥} (hf : IsMulHom f) (hg : IsMulHom g) : IsMulHom (g ‚àò f) :=
  { map_mul := fun x y => by simp only [Function.comp, hf.map_mul, hg.map_mul] }
                             -- üéâ no goals
#align is_mul_hom.comp IsMulHom.comp
#align is_add_hom.comp IsAddHom.comp

/-- A product of maps which preserve multiplication,
preserves multiplication when the target is commutative. -/
@[to_additive
      "A sum of maps which preserves addition, preserves addition when the target
      is commutative."]
theorem mul {Œ± Œ≤} [Semigroup Œ±] [CommSemigroup Œ≤] {f g : Œ± ‚Üí Œ≤} (hf : IsMulHom f)
    (hg : IsMulHom g) : IsMulHom fun a => f a * g a :=
  { map_mul := fun a b => by
      simp only [hf.map_mul, hg.map_mul, mul_comm, mul_assoc, mul_left_comm] }
      -- üéâ no goals
#align is_mul_hom.mul IsMulHom.mul
#align is_add_hom.add IsAddHom.add

/-- The inverse of a map which preserves multiplication,
preserves multiplication when the target is commutative. -/
@[to_additive
      "The negation of a map which preserves addition, preserves addition when
      the target is commutative."]
theorem inv {Œ± Œ≤} [Mul Œ±] [CommGroup Œ≤] {f : Œ± ‚Üí Œ≤} (hf : IsMulHom f) : IsMulHom fun a => (f a)‚Åª¬π :=
  { map_mul := fun a b => (hf.map_mul a b).symm ‚ñ∏ mul_inv _ _ }
#align is_mul_hom.inv IsMulHom.inv
#align is_add_hom.neg IsAddHom.neg

end IsMulHom

/-- Predicate for additive monoid homomorphisms
(deprecated -- use the bundled `MonoidHom` version). -/
structure IsAddMonoidHom [AddZeroClass Œ±] [AddZeroClass Œ≤] (f : Œ± ‚Üí Œ≤) extends IsAddHom f :
  Prop where
  /-- The proposition that `f` preserves the additive identity. -/
  map_zero : f 0 = 0
#align is_add_monoid_hom IsAddMonoidHom

/-- Predicate for monoid homomorphisms (deprecated -- use the bundled `MonoidHom` version). -/
@[to_additive]
structure IsMonoidHom [MulOneClass Œ±] [MulOneClass Œ≤] (f : Œ± ‚Üí Œ≤) extends IsMulHom f : Prop where
  /-- The proposition that `f` preserves the multiplicative identity. -/
  map_one : f 1 = 1
#align is_monoid_hom IsMonoidHom

namespace MonoidHom

variable {M : Type*} {N : Type*} {mM : MulOneClass M} {mN : MulOneClass N}

/-- Interpret a map `f : M ‚Üí N` as a homomorphism `M ‚Üí* N`. -/
@[to_additive "Interpret a map `f : M ‚Üí N` as a homomorphism `M ‚Üí+ N`."]
def of {f : M ‚Üí N} (h : IsMonoidHom f) : M ‚Üí* N
    where
  toFun := f
  map_one' := h.2
  map_mul' := h.1.1
#align monoid_hom.of MonoidHom.of
#align add_monoid_hom.of AddMonoidHom.of

@[to_additive (attr := simp)]
theorem coe_of {f : M ‚Üí N} (hf : IsMonoidHom f) : ‚áë(MonoidHom.of hf) = f :=
  rfl
#align monoid_hom.coe_of MonoidHom.coe_of
#align add_monoid_hom.coe_of AddMonoidHom.coe_of

@[to_additive]
theorem isMonoidHom_coe (f : M ‚Üí* N) : IsMonoidHom (f : M ‚Üí N) :=
  { map_mul := f.map_mul
    map_one := f.map_one }
#align monoid_hom.is_monoid_hom_coe MonoidHom.isMonoidHom_coe
#align add_monoid_hom.is_add_monoid_hom_coe AddMonoidHom.isAddMonoidHom_coe

end MonoidHom

namespace MulEquiv

variable {M : Type*} {N : Type*} [MulOneClass M] [MulOneClass N]

/-- A multiplicative isomorphism preserves multiplication (deprecated). -/
@[to_additive "An additive isomorphism preserves addition (deprecated)."]
theorem isMulHom (h : M ‚âÉ* N) : IsMulHom h :=
  ‚ü®h.map_mul‚ü©
#align mul_equiv.is_mul_hom MulEquiv.isMulHom
#align add_equiv.is_add_hom AddEquiv.isAddHom

/-- A multiplicative bijection between two monoids is a monoid hom
  (deprecated -- use `MulEquiv.toMonoidHom`). -/
@[to_additive
      "An additive bijection between two additive monoids is an additive
      monoid hom (deprecated). "]
theorem isMonoidHom (h : M ‚âÉ* N) : IsMonoidHom h :=
  { map_mul := h.map_mul
    map_one := h.map_one }
#align mul_equiv.is_monoid_hom MulEquiv.isMonoidHom
#align add_equiv.is_add_monoid_hom AddEquiv.isAddMonoidHom

end MulEquiv

namespace IsMonoidHom

variable [MulOneClass Œ±] [MulOneClass Œ≤] {f : Œ± ‚Üí Œ≤} (hf : IsMonoidHom f)

/-- A monoid homomorphism preserves multiplication. -/
@[to_additive "An additive monoid homomorphism preserves addition."]
theorem map_mul' (x y) : f (x * y) = f x * f y :=
  hf.map_mul x y
#align is_monoid_hom.map_mul IsMonoidHom.map_mul'
#align is_add_monoid_hom.map_add IsAddMonoidHom.map_add'

/-- The inverse of a map which preserves multiplication,
preserves multiplication when the target is commutative. -/
@[to_additive
      "The negation of a map which preserves addition, preserves addition
      when the target is commutative."]
theorem inv {Œ± Œ≤} [MulOneClass Œ±] [CommGroup Œ≤] {f : Œ± ‚Üí Œ≤} (hf : IsMonoidHom f) :
    IsMonoidHom fun a => (f a)‚Åª¬π :=
  { map_one := hf.map_one.symm ‚ñ∏ inv_one
    map_mul := fun a b => (hf.map_mul a b).symm ‚ñ∏ mul_inv _ _ }
#align is_monoid_hom.inv IsMonoidHom.inv
#align is_add_monoid_hom.neg IsAddMonoidHom.neg

end IsMonoidHom

/-- A map to a group preserving multiplication is a monoid homomorphism. -/
@[to_additive "A map to an additive group preserving addition is an additive monoid
homomorphism."]
theorem IsMulHom.to_isMonoidHom [MulOneClass Œ±] [Group Œ≤] {f : Œ± ‚Üí Œ≤} (hf : IsMulHom f) :
    IsMonoidHom f :=
  { map_one := mul_right_eq_self.1 <| by rw [‚Üê hf.map_mul, one_mul]
                                         -- üéâ no goals
    map_mul := hf.map_mul }
#align is_mul_hom.to_is_monoid_hom IsMulHom.to_isMonoidHom
#align is_add_hom.to_is_add_monoid_hom IsAddHom.to_isAddMonoidHom

namespace IsMonoidHom

variable [MulOneClass Œ±] [MulOneClass Œ≤] {f : Œ± ‚Üí Œ≤}

/-- The identity map is a monoid homomorphism. -/
@[to_additive "The identity map is an additive monoid homomorphism."]
theorem id : IsMonoidHom (@id Œ±) :=
  { map_one := rfl
    map_mul := fun _ _ => rfl }
#align is_monoid_hom.id IsMonoidHom.id
#align is_add_monoid_hom.id IsAddMonoidHom.id

/-- The composite of two monoid homomorphisms is a monoid homomorphism. -/
@[to_additive
      "The composite of two additive monoid homomorphisms is an additive monoid
      homomorphism."]
theorem comp (hf : IsMonoidHom f) {Œ≥} [MulOneClass Œ≥] {g : Œ≤ ‚Üí Œ≥} (hg : IsMonoidHom g) :
    IsMonoidHom (g ‚àò f) :=
  { IsMulHom.comp hf.toIsMulHom hg.toIsMulHom with
    map_one := show g _ = 1 by rw [hf.map_one, hg.map_one] }
                               -- üéâ no goals
#align is_monoid_hom.comp IsMonoidHom.comp
#align is_add_monoid_hom.comp IsAddMonoidHom.comp

end IsMonoidHom

namespace IsAddMonoidHom

/-- Left multiplication in a ring is an additive monoid morphism. -/
theorem isAddMonoidHom_mul_left {Œ≥ : Type*} [NonUnitalNonAssocSemiring Œ≥] (x : Œ≥) :
    IsAddMonoidHom fun y : Œ≥ => x * y :=
  { map_zero := mul_zero x
    map_add := fun y z => mul_add x y z }
#align is_add_monoid_hom.is_add_monoid_hom_mul_left IsAddMonoidHom.isAddMonoidHom_mul_left

/-- Right multiplication in a ring is an additive monoid morphism. -/
theorem isAddMonoidHom_mul_right {Œ≥ : Type*} [NonUnitalNonAssocSemiring Œ≥] (x : Œ≥) :
    IsAddMonoidHom fun y : Œ≥ => y * x :=
  { map_zero := zero_mul x
    map_add := fun y z => add_mul y z x }
#align is_add_monoid_hom.is_add_monoid_hom_mul_right IsAddMonoidHom.isAddMonoidHom_mul_right

end IsAddMonoidHom

/-- Predicate for additive group homomorphism (deprecated -- use bundled `MonoidHom`). -/
structure IsAddGroupHom [AddGroup Œ±] [AddGroup Œ≤] (f : Œ± ‚Üí Œ≤) extends IsAddHom f : Prop
#align is_add_group_hom IsAddGroupHom

/-- Predicate for group homomorphisms (deprecated -- use bundled `MonoidHom`). -/
@[to_additive]
structure IsGroupHom [Group Œ±] [Group Œ≤] (f : Œ± ‚Üí Œ≤) extends IsMulHom f : Prop
#align is_group_hom IsGroupHom

@[to_additive]
theorem MonoidHom.isGroupHom {G H : Type*} {_ : Group G} {_ : Group H} (f : G ‚Üí* H) :
    IsGroupHom (f : G ‚Üí H) :=
  { map_mul := f.map_mul }
#align monoid_hom.is_group_hom MonoidHom.isGroupHom
#align add_monoid_hom.is_add_group_hom AddMonoidHom.isAddGroupHom

@[to_additive]
theorem MulEquiv.isGroupHom {G H : Type*} {_ : Group G} {_ : Group H} (h : G ‚âÉ* H) :
    IsGroupHom h :=
  { map_mul := h.map_mul }
#align mul_equiv.is_group_hom MulEquiv.isGroupHom
#align add_equiv.is_add_group_hom AddEquiv.isAddGroupHom

/-- Construct `IsGroupHom` from its only hypothesis. -/
@[to_additive "Construct `IsAddGroupHom` from its only hypothesis."]
theorem IsGroupHom.mk' [Group Œ±] [Group Œ≤] {f : Œ± ‚Üí Œ≤} (hf : ‚àÄ x y, f (x * y) = f x * f y) :
    IsGroupHom f :=
  { map_mul := hf }
#align is_group_hom.mk' IsGroupHom.mk'
#align is_add_group_hom.mk' IsAddGroupHom.mk'

namespace IsGroupHom

variable [Group Œ±] [Group Œ≤] {f : Œ± ‚Üí Œ≤} (hf : IsGroupHom f)

open IsMulHom (map_mul)

theorem map_mul' : ‚àÄ x y, f (x * y) = f x * f y :=
  hf.toIsMulHom.map_mul
#align is_group_hom.map_mul IsGroupHom.map_mul'

/-- A group homomorphism is a monoid homomorphism. -/
@[to_additive "An additive group homomorphism is an additive monoid homomorphism."]
theorem to_isMonoidHom : IsMonoidHom f :=
  hf.toIsMulHom.to_isMonoidHom
#align is_group_hom.to_is_monoid_hom IsGroupHom.to_isMonoidHom
#align is_add_group_hom.to_is_add_monoid_hom IsAddGroupHom.to_isAddMonoidHom

/-- A group homomorphism sends 1 to 1. -/
@[to_additive "An additive group homomorphism sends 0 to 0."]
theorem map_one : f 1 = 1 :=
  hf.to_isMonoidHom.map_one
#align is_group_hom.map_one IsGroupHom.map_one
#align is_add_group_hom.map_zero IsAddGroupHom.map_zero

/-- A group homomorphism sends inverses to inverses. -/
@[to_additive "An additive group homomorphism sends negations to negations."]
theorem map_inv (hf : IsGroupHom f) (a : Œ±) : f a‚Åª¬π = (f a)‚Åª¬π :=
  eq_inv_of_mul_eq_one_left <| by rw [‚Üê hf.map_mul, inv_mul_self, hf.map_one]
                                  -- üéâ no goals
#align is_group_hom.map_inv IsGroupHom.map_inv
#align is_add_group_hom.map_neg IsAddGroupHom.map_neg

@[to_additive]
theorem map_div (hf : IsGroupHom f) (a b : Œ±) : f (a / b) = f a / f b := by
  simp_rw [div_eq_mul_inv, hf.map_mul, hf.map_inv]
  -- üéâ no goals
#align is_group_hom.map_div IsGroupHom.map_div
#align is_add_group_hom.map_sub IsAddGroupHom.map_sub

/-- The identity is a group homomorphism. -/
@[to_additive "The identity is an additive group homomorphism."]
theorem id : IsGroupHom (@id Œ±) :=
  { map_mul := fun _ _ => rfl }
#align is_group_hom.id IsGroupHom.id
#align is_add_group_hom.id IsAddGroupHom.id

/-- The composition of two group homomorphisms is a group homomorphism. -/
@[to_additive
      "The composition of two additive group homomorphisms is an additive
      group homomorphism."]
theorem comp (hf : IsGroupHom f) {Œ≥} [Group Œ≥] {g : Œ≤ ‚Üí Œ≥} (hg : IsGroupHom g) :
    IsGroupHom (g ‚àò f) :=
  { IsMulHom.comp hf.toIsMulHom hg.toIsMulHom with }
#align is_group_hom.comp IsGroupHom.comp
#align is_add_group_hom.comp IsAddGroupHom.comp

/-- A group homomorphism is injective iff its kernel is trivial. -/
@[to_additive "An additive group homomorphism is injective if its kernel is trivial."]
theorem injective_iff {f : Œ± ‚Üí Œ≤} (hf : IsGroupHom f) :
    Function.Injective f ‚Üî ‚àÄ a, f a = 1 ‚Üí a = 1 :=
  ‚ü®fun h _ => by rw [‚Üê hf.map_one]; exact @h _ _, fun h x y hxy =>
                 -- ‚ä¢ f x‚úù = f 1 ‚Üí x‚úù = 1
                                    -- üéâ no goals
    eq_of_div_eq_one <| h _ <| by rwa [hf.map_div, div_eq_one]‚ü©
                                  -- üéâ no goals
#align is_group_hom.injective_iff IsGroupHom.injective_iff
#align is_add_group_hom.injective_iff IsAddGroupHom.injective_iff

/-- The product of group homomorphisms is a group homomorphism if the target is commutative. -/
@[to_additive
      "The sum of two additive group homomorphisms is an additive group homomorphism
      if the target is commutative."]
theorem mul {Œ± Œ≤} [Group Œ±] [CommGroup Œ≤] {f g : Œ± ‚Üí Œ≤} (hf : IsGroupHom f) (hg : IsGroupHom g) :
    IsGroupHom fun a => f a * g a :=
  { map_mul := (hf.toIsMulHom.mul hg.toIsMulHom).map_mul }
#align is_group_hom.mul IsGroupHom.mul
#align is_add_group_hom.add IsAddGroupHom.add

/-- The inverse of a group homomorphism is a group homomorphism if the target is commutative. -/
@[to_additive
      "The negation of an additive group homomorphism is an additive group homomorphism
      if the target is commutative."]
theorem inv {Œ± Œ≤} [Group Œ±] [CommGroup Œ≤] {f : Œ± ‚Üí Œ≤} (hf : IsGroupHom f) :
    IsGroupHom fun a => (f a)‚Åª¬π :=
  { map_mul := hf.toIsMulHom.inv.map_mul }
#align is_group_hom.inv IsGroupHom.inv
#align is_add_group_hom.neg IsAddGroupHom.neg

end IsGroupHom

namespace RingHom

/-!
These instances look redundant, because `Deprecated.Ring` provides `IsRingHom` for a `‚Üí+*`.
Nevertheless these are harmless, and helpful for stripping out dependencies on `Deprecated.Ring`.
-/


variable {R : Type*} {S : Type*}

section

variable [NonAssocSemiring R] [NonAssocSemiring S]

theorem to_isMonoidHom (f : R ‚Üí+* S) : IsMonoidHom f :=
  { map_one := f.map_one
    map_mul := f.map_mul }
#align ring_hom.to_is_monoid_hom RingHom.to_isMonoidHom

theorem to_isAddMonoidHom (f : R ‚Üí+* S) : IsAddMonoidHom f :=
  { map_zero := f.map_zero
    map_add := f.map_add }
#align ring_hom.to_is_add_monoid_hom RingHom.to_isAddMonoidHom

end

section

variable [Ring R] [Ring S]

theorem to_isAddGroupHom (f : R ‚Üí+* S) : IsAddGroupHom f :=
  { map_add := f.map_add }
#align ring_hom.to_is_add_group_hom RingHom.to_isAddGroupHom

end

end RingHom

/-- Inversion is a group homomorphism if the group is commutative. -/
@[to_additive
      "Negation is an `AddGroup` homomorphism if the `AddGroup` is commutative."]
theorem Inv.isGroupHom [CommGroup Œ±] : IsGroupHom (Inv.inv : Œ± ‚Üí Œ±) :=
  { map_mul := mul_inv }
#align inv.is_group_hom Inv.isGroupHom
#align neg.is_add_group_hom Neg.isAddGroupHom

/-- The difference of two additive group homomorphisms is an additive group
homomorphism if the target is commutative. -/
theorem IsAddGroupHom.sub {Œ± Œ≤} [AddGroup Œ±] [AddCommGroup Œ≤] {f g : Œ± ‚Üí Œ≤} (hf : IsAddGroupHom f)
    (hg : IsAddGroupHom g) : IsAddGroupHom fun a => f a - g a := by
  simpa only [sub_eq_add_neg] using hf.add hg.neg
  -- üéâ no goals
#align is_add_group_hom.sub IsAddGroupHom.sub

namespace Units

variable {M : Type*} {N : Type*} [Monoid M] [Monoid N]

/-- The group homomorphism on units induced by a multiplicative morphism. -/
@[reducible]
def map' {f : M ‚Üí N} (hf : IsMonoidHom f) : MÀ£ ‚Üí* NÀ£ :=
  map (MonoidHom.of hf)
#align units.map' Units.map'

@[simp]
theorem coe_map' {f : M ‚Üí N} (hf : IsMonoidHom f) (x : MÀ£) : ‚Üë((map' hf : MÀ£ ‚Üí NÀ£) x) = f x :=
  rfl
#align units.coe_map' Units.coe_map'

theorem coe_isMonoidHom : IsMonoidHom (‚Üë¬∑ : MÀ£ ‚Üí M) :=
  (coeHom M).isMonoidHom_coe
#align units.coe_is_monoid_hom Units.coe_isMonoidHom

end Units

namespace IsUnit

variable {M : Type*} {N : Type*} [Monoid M] [Monoid N] {x : M}

theorem map' {f : M ‚Üí N} (hf : IsMonoidHom f) {x : M} (h : IsUnit x) : IsUnit (f x) :=
  h.map (MonoidHom.of hf)
#align is_unit.map' IsUnit.map'

end IsUnit

theorem Additive.isAddHom [Mul Œ±] [Mul Œ≤] {f : Œ± ‚Üí Œ≤} (hf : IsMulHom f) :
    @IsAddHom (Additive Œ±) (Additive Œ≤) _ _ f :=
  { map_add := hf.map_mul }
#align additive.is_add_hom Additive.isAddHom

theorem Multiplicative.isMulHom [Add Œ±] [Add Œ≤] {f : Œ± ‚Üí Œ≤} (hf : IsAddHom f) :
    @IsMulHom (Multiplicative Œ±) (Multiplicative Œ≤) _ _ f :=
  { map_mul := hf.map_add }
#align multiplicative.is_mul_hom Multiplicative.isMulHom

-- defeq abuse
theorem Additive.isAddMonoidHom [MulOneClass Œ±] [MulOneClass Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : IsMonoidHom f) : @IsAddMonoidHom (Additive Œ±) (Additive Œ≤) _ _ f :=
  { Additive.isAddHom hf.toIsMulHom with map_zero := hf.map_one }
#align additive.is_add_monoid_hom Additive.isAddMonoidHom

theorem Multiplicative.isMonoidHom [AddZeroClass Œ±] [AddZeroClass Œ≤] {f : Œ± ‚Üí Œ≤}
    (hf : IsAddMonoidHom f) : @IsMonoidHom (Multiplicative Œ±) (Multiplicative Œ≤) _ _ f :=
  { Multiplicative.isMulHom hf.toIsAddHom with map_one := hf.map_zero }
#align multiplicative.is_monoid_hom Multiplicative.isMonoidHom

theorem Additive.isAddGroupHom [Group Œ±] [Group Œ≤] {f : Œ± ‚Üí Œ≤} (hf : IsGroupHom f) :
    @IsAddGroupHom (Additive Œ±) (Additive Œ≤) _ _ f :=
  { map_add := hf.toIsMulHom.map_mul }
#align additive.is_add_group_hom Additive.isAddGroupHom

theorem Multiplicative.isGroupHom [AddGroup Œ±] [AddGroup Œ≤] {f : Œ± ‚Üí Œ≤} (hf : IsAddGroupHom f) :
    @IsGroupHom (Multiplicative Œ±) (Multiplicative Œ≤) _ _ f :=
  { map_mul := hf.toIsAddHom.map_add }
#align multiplicative.is_group_hom Multiplicative.isGroupHom
