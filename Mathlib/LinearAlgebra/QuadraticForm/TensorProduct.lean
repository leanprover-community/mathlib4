/-
Copyright (c) 2023 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import Mathlib.LinearAlgebra.BilinearForm.TensorProduct
import Mathlib.LinearAlgebra.QuadraticForm.Basic

/-!
# The quadratic form on a tensor product

## Main definitions

* `QuadraticForm.tensorDistrib (Q‚ÇÅ ‚äó‚Çú Q‚ÇÇ)`: the quadratic form on `M‚ÇÅ ‚äó M‚ÇÇ` constructed by applying
  `Q‚ÇÅ` on `M‚ÇÅ` and `Q‚ÇÇ` on `M‚ÇÇ`. This construction is not available in characteristic two.

-/

universe uR uA uM‚ÇÅ uM‚ÇÇ

variable {R : Type uR} {A : Type uA} {M‚ÇÅ : Type uM‚ÇÅ} {M‚ÇÇ : Type uM‚ÇÇ}

open TensorProduct

namespace QuadraticForm

section CommRing
variable [CommRing R] [CommRing A]
variable [AddCommGroup M‚ÇÅ] [AddCommGroup M‚ÇÇ]
variable [Algebra R A] [Module R M‚ÇÅ] [Module A M‚ÇÅ]
variable [SMulCommClass R A M‚ÇÅ] [SMulCommClass A R M‚ÇÅ] [IsScalarTower R A M‚ÇÅ]
variable [Module R M‚ÇÇ] [Invertible (2 : R)]


variable (R A) in
/-- The tensor product of two quadratic forms injects into quadratic forms on tensor products.

Note this is heterobasic; the quadratic form on the left can take values in a larger ring than
the one on the right. -/
def tensorDistrib : QuadraticForm A M‚ÇÅ ‚äó[R] QuadraticForm R M‚ÇÇ ‚Üí‚Çó[A] QuadraticForm A (M‚ÇÅ ‚äó[R] M‚ÇÇ) :=
  letI : Invertible (2 : A) := (Invertible.map (algebraMap R A) 2).copy 2 (map_ofNat _ _).symm
  -- while `letI`s would produce a better term than `let`, they would make this already-slow
  -- definition even slower.
  let toQ := BilinForm.toQuadraticFormLinearMap A A (M‚ÇÅ ‚äó[R] M‚ÇÇ)
  let tmulB := BilinForm.tensorDistrib R A (M‚ÇÅ := M‚ÇÅ) (M‚ÇÇ := M‚ÇÇ)
  let toB := AlgebraTensorModule.map
      (QuadraticForm.associated : QuadraticForm A M‚ÇÅ ‚Üí‚Çó[A] BilinForm A M‚ÇÅ)
      (QuadraticForm.associated : QuadraticForm R M‚ÇÇ ‚Üí‚Çó[R] BilinForm R M‚ÇÇ)
  toQ ‚àò‚Çó tmulB ‚àò‚Çó toB

-- TODO: make the RHS `MulOpposite.op (Q‚ÇÇ m‚ÇÇ) ‚Ä¢ Q‚ÇÅ m‚ÇÅ` so that this has a nicer defeq for
-- `R = A` of `Q‚ÇÅ m‚ÇÅ * Q‚ÇÇ m‚ÇÇ`.
@[simp]
theorem tensorDistrib_tmul (Q‚ÇÅ : QuadraticForm A M‚ÇÅ) (Q‚ÇÇ : QuadraticForm R M‚ÇÇ) (m‚ÇÅ : M‚ÇÅ) (m‚ÇÇ : M‚ÇÇ) :
    tensorDistrib R A (Q‚ÇÅ ‚äó‚Çú Q‚ÇÇ) (m‚ÇÅ ‚äó‚Çú m‚ÇÇ) = Q‚ÇÇ m‚ÇÇ ‚Ä¢ Q‚ÇÅ m‚ÇÅ :=
  letI : Invertible (2 : A) := (Invertible.map (algebraMap R A) 2).copy 2 (map_ofNat _ _).symm
  (BilinForm.tensorDistrib_tmul _ _ _ _ _ _).trans <| congr_arg‚ÇÇ _
    (associated_eq_self_apply _ _ _) (associated_eq_self_apply _ _ _)

/-- The tensor product of two quadratic forms, a shorthand for dot notation. -/
protected abbrev tmul (Q‚ÇÅ : QuadraticForm A M‚ÇÅ) (Q‚ÇÇ : QuadraticForm R M‚ÇÇ) :
    QuadraticForm A (M‚ÇÅ ‚äó[R] M‚ÇÇ) :=
  tensorDistrib R A (Q‚ÇÅ ‚äó‚Çú[R] Q‚ÇÇ)

theorem associated_tmul [Invertible (2 : A)] (Q‚ÇÅ : QuadraticForm A M‚ÇÅ) (Q‚ÇÇ : QuadraticForm R M‚ÇÇ) :
    associated (R‚ÇÅ := A) (Q‚ÇÅ.tmul Q‚ÇÇ)
      = (associated (R‚ÇÅ := A) Q‚ÇÅ).tmul (associated (R‚ÇÅ := R) Q‚ÇÇ) := by
  rw [QuadraticForm.tmul, tensorDistrib, BilinForm.tmul]
  -- ‚ä¢ ‚Üëassociated
  dsimp
  -- ‚ä¢ ‚Üëassociated (‚Üë(BilinForm.toQuadraticFormLinearMap A A (M‚ÇÅ ‚äó[R] M‚ÇÇ)) (‚Üë(Bilin ‚Ä¶
  convert associated_left_inverse A ((associated_isSymm A Q‚ÇÅ).tmul (associated_isSymm R Q‚ÇÇ))
  -- üéâ no goals

variable (A) in
/-- The base change of a quadratic form. -/
protected def baseChange (Q : QuadraticForm R M‚ÇÇ) : QuadraticForm A (A ‚äó[R] M‚ÇÇ) :=
  QuadraticForm.tmul (R := R) (A := A) (M‚ÇÅ := A) (M‚ÇÇ := M‚ÇÇ) (QuadraticForm.sq (R := A)) Q

@[simp]
theorem baseChange_tmul (Q : QuadraticForm R M‚ÇÇ) (a : A) (m‚ÇÇ : M‚ÇÇ) :
    Q.baseChange A (a ‚äó‚Çú m‚ÇÇ) = Q m‚ÇÇ ‚Ä¢ (a * a) :=
  tensorDistrib_tmul _ _ _ _


theorem associated_baseChange [Invertible (2 : A)] (Q : QuadraticForm R M‚ÇÇ)  :
    associated (R‚ÇÅ := A) (Q.baseChange A) = (associated (R‚ÇÅ := R) Q).baseChange A := by
  dsimp only [QuadraticForm.baseChange, BilinForm.baseChange]
  -- ‚ä¢ ‚Üëassociated (QuadraticForm.tmul sq Q) = BilinForm.tmul (‚ÜëLinearMap.toBilin ( ‚Ä¶
  rw [associated_tmul (QuadraticForm.sq (R := A)) Q, associated_sq]
  -- üéâ no goals

end CommRing

end QuadraticForm
