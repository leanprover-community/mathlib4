/-
Copyright (c) 2026 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/

module

public import Mathlib.LinearAlgebra.QuadraticForm.Radical

/-!
# Signature of a quadratic form

We define the signature of a quadratic form over a linearly ordered field, and show that it can be
computed from any sum-of-squares representation.

## Main results and definitions

* `QuadraticForm.sigPos`, `QuadraticForm.sigNeg`: for a quadratic form `Q`, the maximal dimension
  of a subspace on which `Q` is positive-definite (resp. negative-definite).
* `QuadraticForm.sigPos_of_equiv_weightedSumOfSquares`,
  `QuadraticForm.sigNeg_of_equiv_weightedSumOfSquares`: for any isomorphism from `Q` to a
  weighted sum of squares, `Q.sigPos` and `Q.sigNeg` are the number of positive and negative
  weights. (This is the uniqueness part of **Sylvester's law of inertia**; the existence is proved
  elsewhere.)

## Acknowledgements

This file is based on work carried out by Sina Keller, Philipp Schumann, and Nicolas Trutmann in
the course of their studies at ETH ZÃ¼rich.

-/
open Finset QuadraticMap

@[expose] public noncomputable section

variable {R M M' : Type*} [AddCommGroup M] [AddCommGroup M']

section LinearOrder

variable [CommRing R] [LinearOrder R] [Module R M] (Q : QuadraticForm R M)
  [Module R M'] {Q' : QuadraticForm R M'} {V : Submodule R M}

section Equiv
variable {Q}

@[simp] lemma QuadraticMap.IsometryEquiv.map_posDef_iff (e : IsometryEquiv Q Q') :
    (Q'.restrict (V.map e.toLinearMap)).PosDef â†” (Q.restrict V).PosDef := by
  simp [PosDef, -Submodule.mem_map_equiv]

@[simp] lemma QuadraticMap.IsometryEquiv.map_negDef_iff (e : IsometryEquiv Q Q') :
    ((-Q').restrict (V.map e.toLinearMap)).PosDef â†” ((-Q).restrict V).PosDef := by
  simp [PosDef, -Submodule.mem_map_equiv]

end Equiv

open Classical in
/-- The maximal rank of a positive-definite submodule of `M`. -/
-- Note this proof is absurdly overcomplicated in order to avoid assuming `Nontrivial R`.
noncomputable def sigPos : â„• := max'
  {r âˆˆ Iic (Module.finrank R M) | âˆƒ V : Submodule R M,
    Module.finrank R V = r âˆ§ (Q.restrict V).PosDef}
  âŸ¨if Nontrivial R then 0 else 1, by
    split_ifs with h
    Â· simp only [mem_filter, mem_Iic, zero_le, true_and]
      exact âŸ¨âŠ¥, finrank_bot _ _, fun x hx' â†¦ (hx' <| Subsingleton.elim x 0).elimâŸ©
    Â· have : Subsingleton R := not_nontrivial_iff_subsingleton.mp h
      simp only [mem_filter, mem_Iic, Module.finrank_subsingleton, true_and, le_refl]
      exact âŸ¨âŠ¥, fun x hx' â†¦ (hx' <| Subsingleton.elim x 0).elimâŸ©âŸ©

lemma sigPos_le_finrank : sigPos Q â‰¤ Module.finrank R M := by
  classical
  exact mem_Iic.mp <| mem_of_mem_filter _ <| max'_mem _ _

/-- Defining property of `sigPos`. -/
lemma sigPos_isGreatest [Module.Finite R M] [StrongRankCondition R] : IsGreatest
    {r | âˆƒ V : Submodule R M, Module.finrank R V = r âˆ§ (Q.restrict V).PosDef} (sigPos Q) := by
  classical
  refine âŸ¨(mem_filter.mp <| max'_mem _ _).2, ?_âŸ©
  rintro _ âŸ¨V, rfl, hVâŸ©
  apply le_max'
  rw [mem_filter, mem_Iic]
  exact âŸ¨V.finrank_le, V, rfl, hVâŸ©

open Classical in
/-- The maximal dimension of a negative-definite subspace of `M`. -/
noncomputable def sigNeg : â„• := sigPos (-Q)

/-- Defining property of `sigNeg`. -/
lemma sigNeg_isGreatest [Module.Finite R M] [StrongRankCondition R] : IsGreatest
    {r | âˆƒ V : Submodule R M, Module.finrank R V = r âˆ§ ((-Q).restrict V).PosDef} (sigNeg Q) :=
  sigPos_isGreatest (-Q)

variable {Q}

lemma QuadraticMap.Equivalent.sigPos_eq (h : Equivalent Q Q') : sigPos Q = sigPos Q' := by
  obtain âŸ¨eâŸ© := h
  unfold sigPos
  congr! with j
  Â· apply (Submodule.orderIsoMapComap e.toLinearEquiv).exists_congr
    intro V
    refine .and ?_ (IsometryEquiv.map_posDef_iff _).symm
    revert j
    rw [eq_iff_eq_cancel_right]
    exact (e.finrank_map_eq _).symm
  Â· exact e.toLinearEquiv.finrank_eq

lemma QuadraticMap.Equivalent.sigNeg_eq (h : Equivalent Q Q') : sigNeg Q = sigNeg Q' :=
  sigPos_eq <| match h with | âŸ¨eâŸ© => âŸ¨e, by simpâŸ©

end LinearOrder

section Field
namespace QuadraticForm

variable {ğ•œ : Type*} [Field ğ•œ] [LinearOrder ğ•œ]
  [Module ğ•œ M] [Module ğ•œ M'] {Q : QuadraticForm ğ•œ M}

/-- Key lemma for Sylvester's law of inertia: the sum of `sigPos Q` and the dimension of any
negative-semidefinite subspace is bounded above by the dimension of the whole space. -/
lemma sigPos_add_finrank_le_of_nonpos [FiniteDimensional ğ•œ M]
    {V : Subspace ğ•œ M} (hV : âˆ€ x âˆˆ V, Q x â‰¤ 0) :
    sigPos Q + Module.finrank ğ•œ V â‰¤ Module.finrank ğ•œ M := by
  obtain âŸ¨Vp, hr, hVpâŸ© := (sigPos_isGreatest Q).1
  rw [â† hr]
  apply Submodule.finrank_add_finrank_le_of_disjoint
  intro W hWp hWm
  rw [le_bot_iff, Submodule.eq_bot_iff]
  intro x hx
  by_contra hx'
  have := hVp âŸ¨x, hWp hxâŸ© (by simpa using hx')
  have := hV x (hWm hx)
  simp_all only [restrict_apply]
  grind

variable {Î¹ : Type*} [Fintype Î¹] {w : Î¹ â†’ ğ•œ} [IsStrictOrderedRing ğ•œ]

private lemma posDef_spanSubset (s : Set Î¹) (hs : âˆ€ i âˆˆ s, 0 < w i) :
    (weightedSumSquares ğ•œ w).restrict (Pi.spanSubset ğ•œ s) |>.PosDef := by
  intro âŸ¨v, hvâŸ© hv'
  rw [restrict_apply, weightedSumSquares_apply]
  apply sum_pos'
  Â· intro i _
    by_cases hi : i âˆˆ s
    Â· exact smul_nonneg (hs i hi).le (mul_self_nonneg _)
    Â· simp [Pi.mem_spanSubset_iff.mp hv i hi]
  Â· simp only [ne_eq, Submodule.mk_eq_zero, funext_iff, not_forall, Pi.zero_apply] at hv'
    obtain âŸ¨i, hiâŸ© := hv'
    refine âŸ¨i, mem_univ _, ?_âŸ©
    have : i âˆˆ s := by
      contrapose hi
      exact Pi.mem_spanSubset_iff.mp hv i hi
    exact smul_pos (hs i this) (mul_self_pos.mpr hi)

private lemma negSemidef_spanSubset (s : Set Î¹) (hs : âˆ€ i âˆˆ s, w i â‰¤ 0) :
    âˆ€ x âˆˆ Pi.spanSubset ğ•œ s, (weightedSumSquares ğ•œ w) x â‰¤ 0 := by
  intro x hx
  simp only [weightedSumSquares_apply, smul_eq_mul]
  apply sum_nonpos
  intro i _
  by_cases hi : i âˆˆ s
  Â· exact mul_nonpos_of_nonpos_of_nonneg (hs i hi) (mul_self_nonneg _)
  Â· rw [Pi.mem_spanSubset_iff.mp hx i hi, mul_zero, mul_zero]

/-- Key lemma for Sylvester's law of inertia: compute the signature of a weighted sum of squares. -/
lemma sigPos_weightedSumSquares :
    sigPos (weightedSumSquares ğ•œ w) = {i | 0 < w i}.ncard := by
  classical
  let p : Set Î¹ := {i | 0 < w i}
  let m : Set Î¹ := {i | w i â‰¤ 0}
  convert_to sigPos _ = p.ncard
  have : p.ncard + m.ncard = Nat.card Î¹ := by
    convert Set.ncard_add_ncard_compl p
    ext
    grind
  have : p.ncard â‰¤ sigPos (weightedSumSquares ğ•œ w) :=
    (sigPos_isGreatest _).2 âŸ¨Pi.spanSubset ğ•œ p, Pi.dim_spanSubset,
      posDef_spanSubset p (by grind)âŸ©
  suffices sigPos (weightedSumSquares ğ•œ w) + m.ncard â‰¤ Nat.card Î¹ by lia
  simpa using sigPos_add_finrank_le_of_nonpos <| negSemidef_spanSubset m (fun _ hi â†¦ hi)

lemma sigNeg_weightedSumSquares :
    sigNeg (weightedSumSquares ğ•œ w) = {i | w i < 0}.ncard := by
  simp only [sigNeg]
  convert sigPos_weightedSumSquares (w := -w) using 2
  Â· ext; simp
  Â· simp

private lemma sigPos_add_sigNeg_add_radicalâ‚ :
    sigPos (weightedSumSquares ğ•œ w) + sigNeg (weightedSumSquares ğ•œ w) +
      Module.finrank ğ•œ (weightedSumSquares ğ•œ w).radical = Nat.card Î¹ := by
  classical
  rw [radical_weightedSumSquares, sigPos_weightedSumSquares, sigNeg_weightedSumSquares,
    Pi.dim_spanSubset]
  calc {i | 0 < w i}.ncard + {i | w i < 0}.ncard + {i | w i = 0}.ncard
  _ = {i | 0 < w i}.ncard + {i | w i â‰¤ 0}.ncard := by
    rw [add_assoc, add_left_cancel_iff, â† Set.ncard_union_eq]
    Â· congr! 1
      ext
      grind
    Â· grind [disjoint_iff_ne]
  _ = Set.univ.ncard := by
    rw [â† Set.ncard_union_eq]
    Â· congr! 1
      ext
      grind [le_iff_lt_or_eq]
    Â· grind [disjoint_iff_ne]
  _ = Nat.card Î¹ := Set.ncard_univ _

lemma sigPos_add_sigNeg_add_radical [FiniteDimensional ğ•œ M] :
    sigPos Q + sigNeg Q + Module.finrank ğ•œ Q.radical = Module.finrank ğ•œ M := by
  have : Invertible (2 : ğ•œ) := invertibleOfNonzero (NeZero.ne _)
  obtain âŸ¨w, eâŸ© := Q.equivalent_weightedSumSquares
  rw [e.sigPos_eq, e.sigNeg_eq, e.rank_radical_eq]
  convert QuadraticForm.sigPos_add_sigNeg_add_radicalâ‚ (w := w)
  exact Eq.symm (Nat.card_fin (Module.finrank ğ•œ M))

lemma sigPos_of_equiv_weightedSumSquares (hQ : Equivalent Q (weightedSumSquares ğ•œ w)) :
    sigPos Q = {i | 0 < w i}.ncard := by
  rw [hQ.sigPos_eq]
  exact sigPos_weightedSumSquares

lemma sigNeg_of_equiv_weightedSumSquares (hQ : Equivalent Q (weightedSumSquares ğ•œ w)) :
    sigNeg Q = {i | w i < 0}.ncard := by
  rw [hQ.sigNeg_eq]
  exact sigNeg_weightedSumSquares

end QuadraticForm
