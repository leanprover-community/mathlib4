/-
Copyright (c) 2026 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/

module

public import Mathlib.LinearAlgebra.QuadraticForm.Radical

/-!
# Signature of a quadratic form
-/
open Finset QuadraticMap

@[expose] public noncomputable section

variable {R M M' : Type*} [AddCommGroup M] [AddCommGroup M']

section LinearOrder

variable [CommRing R] [LinearOrder R] [Module R M] (Q : QuadraticForm R M)
  [Module R M'] {Q' : QuadraticForm R M'} {V : Submodule R M}

section Equiv
variable {Q}

@[simp] lemma QuadraticMap.IsometryEquiv.map_posDef_iff (e : IsometryEquiv Q Q') :
    (Q'.restrict (V.map e.toLinearMap)).PosDef â†” (Q.restrict V).PosDef := by
  simp [PosDef, -Submodule.mem_map_equiv]

@[simp] lemma QuadraticMap.IsometryEquiv.map_negDef_iff (e : IsometryEquiv Q Q') :
    ((-Q').restrict (V.map e.toLinearMap)).PosDef â†” ((-Q).restrict V).PosDef := by
  simp [PosDef, -Submodule.mem_map_equiv]

end Equiv

open Classical in
/-- The maximal rank of a positive-definite submodule of `M`. -/
-- Note this proof is absurdly overcomplicated in order to avoid assuming `Nontrivial R`.
noncomputable def sigPos : â„• := max'
  {r âˆˆ Iic (Module.finrank R M) | âˆƒ V : Submodule R M,
    Module.finrank R V = r âˆ§ (Q.restrict V).PosDef}
  âŸ¨if Nontrivial R then 0 else 1, by
    split_ifs with h
    Â· simp only [mem_filter, mem_Iic, zero_le, true_and]
      exact âŸ¨âŠ¥, finrank_bot _ _, fun x hx' â†¦ (hx' <| Subsingleton.elim x 0).elimâŸ©
    Â· have : Subsingleton R := not_nontrivial_iff_subsingleton.mp h
      simp only [mem_filter, mem_Iic, Module.finrank_subsingleton, true_and, le_refl]
      exact âŸ¨âŠ¥, fun x hx' â†¦ (hx' <| Subsingleton.elim x 0).elimâŸ©âŸ©

lemma sigPos_le_finrank : sigPos Q â‰¤ Module.finrank R M := by
  classical
  exact mem_Iic.mp <| mem_of_mem_filter _ <| max'_mem _ _

/-- Defining property of `sigPos`. -/
lemma sigPos_isGreatest [Module.Finite R M] [StrongRankCondition R] : IsGreatest
    {r | âˆƒ V : Submodule R M, Module.finrank R V = r âˆ§ (Q.restrict V).PosDef} (sigPos Q) := by
  classical
  refine âŸ¨(mem_filter.mp <| max'_mem _ _).2, ?_âŸ©
  rintro _ âŸ¨V, rfl, hVâŸ©
  apply le_max'
  rw [mem_filter, mem_Iic]
  exact âŸ¨V.finrank_le, V, rfl, hVâŸ©

open Classical in
/-- The maximal dimension of a negative-definite subspace of `M`. -/
noncomputable def sigNeg : â„• := sigPos (-Q)

/-- Defining property of `sigNeg`. -/
lemma sigNeg_isGreatest [Module.Finite R M] [StrongRankCondition R] : IsGreatest
    {r | âˆƒ V : Submodule R M, Module.finrank R V = r âˆ§ ((-Q).restrict V).PosDef} (sigNeg Q) :=
  sigPos_isGreatest (-Q)

variable {Q}

lemma QuadraticMap.Equivalent.sigPos_eq (h : Equivalent Q Q') : sigPos Q = sigPos Q' := by
  obtain âŸ¨eâŸ© := h
  unfold sigPos
  congr! with j
  Â· apply (Submodule.orderIsoMapComap e.toLinearEquiv).exists_congr
    intro V
    refine .and ?_ (IsometryEquiv.map_posDef_iff _).symm
    revert j
    rw [eq_iff_eq_cancel_right]
    exact (e.finrank_map_eq _).symm
  Â· exact e.toLinearEquiv.finrank_eq

lemma QuadraticMap.Equivalent.sigNeg_eq (h : Equivalent Q Q') : sigNeg Q = sigNeg Q' :=
  sigPos_eq <| match h with | âŸ¨eâŸ© => âŸ¨e, by simpâŸ©

end LinearOrder

section Field

variable {ğ•œ : Type*} [Field ğ•œ] [Module ğ•œ M] [Module ğ•œ M']
  {Q : QuadraticForm ğ•œ M}

/-- Key lemma for Sylvester's law of inertia: the sum of `sigPos Q` and the dimension of any
negative-semidefinite subspace is bounded above by the dimension of the whole space. -/
lemma sigPos_add_finrank_le_of_nonpos [LinearOrder ğ•œ] [FiniteDimensional ğ•œ M]
    {V : Subspace ğ•œ M} (hV : âˆ€ x âˆˆ V, Q x â‰¤ 0) :
    sigPos Q + Module.finrank ğ•œ V â‰¤ Module.finrank ğ•œ M := by
  obtain âŸ¨Vp, hr, hVpâŸ© := (sigPos_isGreatest Q).1
  rw [â† hr]
  apply Submodule.finrank_add_finrank_le_of_disjoint
  intro W hWp hWm
  rw [le_bot_iff, Submodule.eq_bot_iff]
  intro x hx
  by_contra hx'
  have := hVp âŸ¨x, hWp hxâŸ© (by simpa using hx')
  have := hV x (hWm hx)
  simp_all only [restrict_apply]
  grind

variable {Î¹ : Type*} [Fintype Î¹] {w : Î¹ â†’ ğ•œ} [DecidableEq ğ•œ]

variable (ğ•œ) in
private abbrev spanOfSet (s : Finset Î¹) : Submodule ğ•œ (Î¹ â†’ ğ•œ) :=
  .span ğ•œ (s.image <| Pi.basisFun ğ•œ Î¹) -- todo: reimplement using `Module.Basis.mem_span_image`?

open Submodule in
private lemma mem_spanOfSet_iff {s : Finset Î¹} {v : Î¹ â†’ ğ•œ} :
    v âˆˆ spanOfSet ğ•œ s â†” âˆ€ i âˆ‰ s, v i = 0 := by
  classical
  constructor
  Â· rw [mem_span_finset']
    rintro âŸ¨f, rflâŸ© i hi
    rw [Finset.sum_apply]
    refine sum_eq_zero fun âŸ¨x, hxâŸ© _ â†¦ ?_
    obtain âŸ¨j, hj, rflâŸ© := mem_image.mp hx
    simp [show i â‰  j by grind]
  Â· intro hv
    suffices v = âˆ‘ i âˆˆ s, v i â€¢ Pi.basisFun ğ•œ Î¹ i by
      rw [this]
      refine Submodule.sum_mem _ (fun j hj â†¦ smul_mem _ (v j) ?_)
      exact mem_span_of_mem <| Finset.mem_coe.2 <| Finset.mem_image_of_mem _ hj
    ext j
    by_cases h : j âˆˆ s
    Â· simp [Pi.single_apply, if_pos h]
    Â· simp [Pi.single_apply, hv j h]

private lemma zero_spanOfSet (s : Finset Î¹)
    (v) (hv : v âˆˆ spanOfSet ğ•œ s) (i) (hi : i âˆ‰ s) : v i = 0 :=
  (mem_spanOfSet_iff.mp hv) i hi

private lemma dim_spanOfSet (s : Finset Î¹) :
    Module.finrank ğ•œ (spanOfSet ğ•œ s) = #s := by
  classical
  rw [finrank_span_finset_eq_card, card_image_of_injective]
  Â· intro x y hxy
    simpa [Pi.single_apply] using _root_.congr_fun hxy x -- missing `protected`?
  Â· simpa using
      (Pi.linearIndependent_single_one _ _).linearIndepOn.mono (by tauto) |>.id_image

private lemma QuadraticForm.radical_sumSq_eq [NeZero (2 : ğ•œ)] :
    radical (weightedSumSquares ğ•œ w) = spanOfSet ğ•œ {i | w i = 0} := by
  classical
  ext v
  simp only [mem_radical_iff', weightedSumSquares_apply, â† pow_two, smul_eq_mul, Pi.add_apply,
    add_sq, mul_add, sum_add_distrib, add_eq_right, mem_spanOfSet_iff, mem_filter, mem_univ,
    true_and]
  constructor
  Â· rintro âŸ¨hv, hvv'âŸ© i
    simp only [hv, zero_add] at hvv'
    specialize hvv' (Pi.single i 1)
    simp_all [Pi.single_apply, NeZero.ne, or_iff_not_imp_left]
  Â· refine fun h â†¦ âŸ¨?_, fun v â†¦ ?_âŸ© <;> [skip ; simp only [â† sum_add_distrib]] <;>
    Â· apply sum_eq_zero
      grind [mul_eq_zero]

variable [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]

private lemma posDef_spanOfSet (s : Finset Î¹) (hs : âˆ€ i âˆˆ s, 0 < w i) :
    (weightedSumSquares ğ•œ w).restrict (spanOfSet ğ•œ s) |>.PosDef := by
  intro âŸ¨v, hvâŸ© hv'
  rw [restrict_apply, weightedSumSquares_apply]
  apply sum_pos'
  Â· intro i _
    by_cases hi : i âˆˆ s
    Â· exact smul_nonneg (hs i hi).le (mul_self_nonneg _)
    Â· simp [zero_spanOfSet s v hv i hi]
  Â· simp only [ne_eq, Submodule.mk_eq_zero, funext_iff, not_forall, Pi.zero_apply] at hv'
    obtain âŸ¨i, hiâŸ© := hv'
    refine âŸ¨i, mem_univ _, ?_âŸ©
    have : i âˆˆ s := by
      contrapose hi
      exact zero_spanOfSet s v hv i hi
    exact smul_pos (hs i this) (mul_self_pos.mpr hi)

private lemma negSemidef_spanOfSet (s : Finset Î¹) (hs : âˆ€ i âˆˆ s, w i â‰¤ 0) :
    âˆ€ x âˆˆ spanOfSet ğ•œ s, (weightedSumSquares ğ•œ w) x â‰¤ 0 := by
  intro x hx
  simp only [weightedSumSquares_apply, smul_eq_mul]
  apply sum_nonpos
  intro i _
  by_cases hi : i âˆˆ s
  Â· exact mul_nonpos_of_nonpos_of_nonneg (hs i hi) (mul_self_nonneg _)
  Â· rw [zero_spanOfSet s x hx i hi, mul_zero, mul_zero]

omit [DecidableEq ğ•œ]

/-- Key lemma for Sylvester's law of inertia: compute the signature of a weighted sum of squares. -/
lemma QuadraticForm.sigPos_sumSq_eq :
    sigPos (weightedSumSquares ğ•œ w) = #{i | 0 < w i} := by
  classical
  let p : Finset Î¹ := {i | 0 < w i}
  let m : Finset Î¹ := {i | w i â‰¤ 0}
  convert_to sigPos _ = #p
  have : #p + #m = Fintype.card Î¹ := by
    convert card_add_card_compl p
    grind [mem_compl]
  have : #p â‰¤ sigPos (weightedSumSquares ğ•œ w) :=
    (sigPos_isGreatest _).2 âŸ¨spanOfSet ğ•œ p, dim_spanOfSet _, posDef_spanOfSet p (by grind)âŸ©
  suffices sigPos (weightedSumSquares ğ•œ w) + #m â‰¤ Fintype.card Î¹ by lia
  have : âˆ€ x âˆˆ spanOfSet ğ•œ m, (weightedSumSquares ğ•œ w) x â‰¤ 0 := negSemidef_spanOfSet m (by aesop)
  convert sigPos_add_finrank_le_of_nonpos this
  Â· exact (dim_spanOfSet m).symm
  Â· simp

lemma QuadraticForm.sigNeg_sumSq_eq :
    sigNeg (weightedSumSquares ğ•œ w) = #{i | w i < 0} := by
  simp only [sigNeg]
  convert sigPos_sumSq_eq (w := -w) using 2
  Â· ext; simp
  Â· simp

lemma QuadraticForm.sigPos_add_sigNeg_add_radical :
    sigPos (weightedSumSquares ğ•œ w) + sigNeg (weightedSumSquares ğ•œ w) +
      Module.finrank ğ•œ (weightedSumSquares ğ•œ w).radical = Fintype.card Î¹ := by
  classical
  rw [radical_sumSq_eq, sigPos_sumSq_eq, sigNeg_sumSq_eq, dim_spanOfSet]
  calc #{i | 0 < w i} + #{i | w i < 0} + #{i | w i = 0}
  _ = #{i | 0 < w i} + #{i | w i â‰¤ 0} := by
    rw [add_assoc, add_left_cancel_iff, â† card_union_of_disjoint]
    Â· congr! 1
      grind
    Â· grind [disjoint_iff_ne]
  _ = #univ := by
    rw [â† card_union_of_disjoint]
    Â· congr! 1
      grind [le_iff_lt_or_eq]
    Â· grind [disjoint_iff_ne]
  _ = Fintype.card Î¹ := card_univ
