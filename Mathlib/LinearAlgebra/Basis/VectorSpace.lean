/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro, Alexander Bentkamp
-/
module

public import Mathlib.LinearAlgebra.FreeModule.Basic
public import Mathlib.LinearAlgebra.LinearIndependent.Lemmas
public import Mathlib.LinearAlgebra.LinearPMap
public import Mathlib.LinearAlgebra.Projection
public import Mathlib.Tactic.Field

/-!
# Bases in a vector space

This file provides results for bases of a vector space.

Some of these results should be merged with the results on free modules.
We state these results in a separate file to the results on modules to avoid an
import cycle.

## Main statements

* `Basis.ofVectorSpace` states that every vector space has a basis.
* `Module.Free.of_divisionRing` states that every vector space is a free module.

## Tags

basis, bases

-/

@[expose] public section

open Function Module Set Submodule

variable {Œπ : Type*} {Œπ' : Type*} {K : Type*} {V : Type*} {V' : Type*}

section DivisionRing

variable [DivisionRing K] [AddCommGroup V] [AddCommGroup V'] [Module K V] [Module K V']
variable {v : Œπ ‚Üí V} {s t : Set V} {x y z : V}

open Submodule

namespace Module.Basis

section ExistsBasis

/-- If `s` is a linear independent set of vectors, we can extend it to a basis. -/
noncomputable def extend (hs : LinearIndepOn K id s) :
    Basis (hs.extend (subset_univ s)) K V :=
  Basis.mk
    (hs.linearIndepOn_extend _).linearIndependent_restrict
    (SetLike.coe_subset_coe.mp <| by simpa using hs.subset_span_extend (subset_univ s))

theorem extend_apply_self (hs : LinearIndepOn K id s) (x : hs.extend _) : Basis.extend hs x = x :=
  Basis.mk_apply _ _ _

@[simp]
theorem coe_extend (hs : LinearIndepOn K id s) : ‚áë(Basis.extend hs) = ((‚Üë) : _ ‚Üí _) :=
  funext (extend_apply_self hs)

theorem range_extend (hs : LinearIndepOn K id s) :
    range (Basis.extend hs) = hs.extend (subset_univ _) := by
  rw [coe_extend, Subtype.range_coe_subtype, setOf_mem_eq]

/-- Auxiliary definition: the index for the new basis vectors in `Basis.sumExtend`.

The specific value of this definition should be considered an implementation detail. -/
def sumExtendIndex (hs : LinearIndependent K v) : Set V :=
  LinearIndepOn.extend hs.linearIndepOn_id (subset_univ _) \ range v

/-- If `v` is a linear independent family of vectors, extend it to a basis indexed by a sum type. -/
noncomputable def sumExtend (hs : LinearIndependent K v) : Basis (Œπ ‚äï sumExtendIndex hs) K V :=
  let s := Set.range v
  let e : Œπ ‚âÉ s := Equiv.ofInjective v hs.injective
  let b := hs.linearIndepOn_id.extend (subset_univ (Set.range v))
  (Basis.extend hs.linearIndepOn_id).reindex <|
    Equiv.symm <|
      calc
        Œπ ‚äï (b \ s : Set V) ‚âÉ s ‚äï (b \ s : Set V) := Equiv.sumCongr e (Equiv.refl _)
        _ ‚âÉ b :=
          haveI := Classical.decPred (¬∑ ‚àà s)
          Equiv.Set.sumDiffSubset (hs.linearIndepOn_id.subset_extend _)

theorem subset_extend {s : Set V} (hs : LinearIndepOn K id s) : s ‚äÜ hs.extend (Set.subset_univ _) :=
  hs.subset_extend _

/-- If `s` is a family of linearly independent vectors contained in a set `t` spanning `V`,
then one can get a basis of `V` containing `s` and contained in `t`. -/
noncomputable def extendLe (hs : LinearIndepOn K id s) (hst : s ‚äÜ t) (ht : ‚ä§ ‚â§ span K t) :
    Basis (hs.extend hst) K V :=
  Basis.mk
    ((hs.linearIndepOn_extend _).linearIndependent ..)
    (le_trans ht <| Submodule.span_le.2 <| by simpa using hs.subset_span_extend hst)

theorem extendLe_apply_self (hs : LinearIndepOn K id s) (hst : s ‚äÜ t) (ht : ‚ä§ ‚â§ span K t)
    (x : hs.extend hst) : Basis.extendLe hs hst ht x = x :=
  Basis.mk_apply _ _ _

@[simp]
theorem coe_extendLe (hs : LinearIndepOn K id s) (hst : s ‚äÜ t) (ht : ‚ä§ ‚â§ span K t) :
    ‚áë(Basis.extendLe hs hst ht) = ((‚Üë) : _ ‚Üí _) :=
  funext (extendLe_apply_self hs hst ht)

theorem range_extendLe (hs : LinearIndepOn K id s) (hst : s ‚äÜ t) (ht : ‚ä§ ‚â§ span K t) :
    range (Basis.extendLe hs hst ht) = hs.extend hst := by
  rw [coe_extendLe, Subtype.range_coe_subtype, setOf_mem_eq]

theorem subset_extendLe (hs : LinearIndepOn K id s) (hst : s ‚äÜ t) (ht : ‚ä§ ‚â§ span K t) :
    s ‚äÜ range (Basis.extendLe hs hst ht) :=
  (range_extendLe hs hst ht).symm ‚ñ∏ hs.subset_extend hst

theorem extendLe_subset (hs : LinearIndepOn K id s) (hst : s ‚äÜ t) (ht : ‚ä§ ‚â§ span K t) :
    range (Basis.extendLe hs hst ht) ‚äÜ t :=
  (range_extendLe hs hst ht).symm ‚ñ∏ hs.extend_subset hst

/-- If a set `s` spans the space, this is a basis contained in `s`. -/
noncomputable def ofSpan (hs : ‚ä§ ‚â§ span K s) :
    Basis ((linearIndepOn_empty K id).extend (empty_subset s)) K V :=
  extendLe (linearIndependent_empty K V) (empty_subset s) hs

theorem ofSpan_apply_self (hs : ‚ä§ ‚â§ span K s)
    (x : (linearIndepOn_empty K id).extend (empty_subset s)) :
    Basis.ofSpan hs x = x :=
  extendLe_apply_self (linearIndependent_empty K V) (empty_subset s) hs x

@[simp]
theorem coe_ofSpan (hs : ‚ä§ ‚â§ span K s) : ‚áë(ofSpan hs) = ((‚Üë) : _ ‚Üí _) :=
  funext (ofSpan_apply_self hs)

theorem range_ofSpan (hs : ‚ä§ ‚â§ span K s) :
    range (ofSpan hs) = (linearIndepOn_empty K id).extend (empty_subset s) := by
  rw [coe_ofSpan, Subtype.range_coe_subtype, setOf_mem_eq]

theorem ofSpan_subset (hs : ‚ä§ ‚â§ span K s) : range (ofSpan hs) ‚äÜ s :=
  extendLe_subset (linearIndependent_empty K V) (empty_subset s) hs

section

variable (K V)

/-- A set used to index `Basis.ofVectorSpace`. -/
noncomputable def ofVectorSpaceIndex : Set V :=
  (linearIndepOn_empty K id).extend (subset_univ _)

/-- Each vector space has a basis. -/
noncomputable def ofVectorSpace : Basis (ofVectorSpaceIndex K V) K V :=
  Basis.extend (linearIndependent_empty K V)

@[stacks 09FN "Generalized from fields to division rings."]
instance (priority := 100) _root_.Module.Free.of_divisionRing : Module.Free K V :=
  Module.Free.of_basis (ofVectorSpace K V)

theorem ofVectorSpace_apply_self (x : ofVectorSpaceIndex K V) : ofVectorSpace K V x = x := by
  unfold ofVectorSpace
  exact Basis.mk_apply _ _ _

@[simp]
theorem coe_ofVectorSpace : ‚áë(ofVectorSpace K V) = ((‚Üë) : _ ‚Üí _) :=
  funext fun x => ofVectorSpace_apply_self K V x

theorem ofVectorSpaceIndex.linearIndependent :
    LinearIndependent K ((‚Üë) : ofVectorSpaceIndex K V ‚Üí V) := by
  convert (ofVectorSpace K V).linearIndependent
  ext x
  rw [ofVectorSpace_apply_self]

theorem range_ofVectorSpace : range (ofVectorSpace K V) = ofVectorSpaceIndex K V :=
  range_extend _

theorem exists_basis : ‚àÉ s : Set V, Nonempty (Basis s K V) :=
  ‚ü®ofVectorSpaceIndex K V, ‚ü®ofVectorSpace K V‚ü©‚ü©

end

end ExistsBasis

end Module.Basis

open Fintype

variable (K V)

theorem VectorSpace.card_fintype [Fintype K] [Fintype V] : ‚àÉ n : ‚Ñï, card V = card K ^ n := by
  classical
  exact ‚ü®card (Basis.ofVectorSpaceIndex K V), Module.card_fintype (Basis.ofVectorSpace K V)‚ü©

section AtomsOfSubmoduleLattice

variable {K V}

/-- For a module over a division ring, the span of a nonzero element is an atom of the
lattice of submodules. -/
theorem nonzero_span_atom (v : V) (hv : v ‚â† 0) : IsAtom (span K {v} : Submodule K V) := by
  constructor
  ¬∑ rw [Submodule.ne_bot_iff]
    exact ‚ü®v, ‚ü®mem_span_singleton_self v, hv‚ü©‚ü©
  ¬∑ intro T hT
    by_contra h
    apply hT.2
    change span K {v} ‚â§ T
    simp_rw [span_singleton_le_iff_mem, ‚Üê Ne.eq_def, Submodule.ne_bot_iff] at *
    rcases h with ‚ü®s, ‚ü®hs, hz‚ü©‚ü©
    rcases mem_span_singleton.1 (hT.1 hs) with ‚ü®a, rfl‚ü©
    rcases eq_or_ne a 0 with rfl | h
    ¬∑ simp only [zero_smul, ne_eq, not_true] at hz
    ¬∑ rwa [T.smul_mem_iff h] at hs

/-- The atoms of the lattice of submodules of a module over a division ring are the
submodules equal to the span of a nonzero element of the module. -/
theorem atom_iff_nonzero_span (W : Submodule K V) :
    IsAtom W ‚Üî ‚àÉ v ‚â† 0, W = span K {v} := by
  refine ‚ü®fun h => ?_, fun h => ?_‚ü©
  ¬∑ obtain ‚ü®hbot, h‚ü© := h
    rcases (Submodule.ne_bot_iff W).1 hbot with ‚ü®v, ‚ü®hW, hv‚ü©‚ü©
    refine ‚ü®v, ‚ü®hv, ?_‚ü©‚ü©
    by_contra heq
    specialize h (span K {v})
    rw [span_singleton_eq_bot, lt_iff_le_and_ne] at h
    exact hv (h ‚ü®(span_singleton_le_iff_mem v W).2 hW, Ne.symm heq‚ü©)
  ¬∑ rcases h with ‚ü®v, ‚ü®hv, rfl‚ü©‚ü©
    exact nonzero_span_atom v hv

/-- The lattice of submodules of a module over a division ring is atomistic. -/
instance : IsAtomistic (Submodule K V) :=
  CompleteLattice.isAtomistic_iff.2 fun W => by
    refine ‚ü®_, submodule_eq_sSup_le_nonzero_spans W, ?_‚ü©
    rintro _ ‚ü®w, ‚ü®_, ‚ü®hw, rfl‚ü©‚ü©‚ü©
    exact nonzero_span_atom w hw

end AtomsOfSubmoduleLattice

variable {K V}

theorem LinearMap.exists_leftInverse_of_injective (f : V ‚Üí‚Çó[K] V') (hf_inj : LinearMap.ker f = ‚ä•) :
    ‚àÉ g : V' ‚Üí‚Çó[K] V, g.comp f = LinearMap.id := by
  let B := Basis.ofVectorSpaceIndex K V
  let hB := Basis.ofVectorSpace K V
  have hB‚ÇÄ : _ := hB.linearIndependent.linearIndepOn_id
  have : LinearIndepOn K _root_.id (f '' B) := by
    have h‚ÇÅ : LinearIndepOn K _root_.id (f '' Set.range (Basis.ofVectorSpace K V)) :=
      LinearIndepOn.image (f := f) hB‚ÇÄ (show Disjoint _ _ by simp [hf_inj])
    rwa [Basis.range_ofVectorSpace K V] at h‚ÇÅ
  let C := this.extend (subset_univ _)
  have BC := this.subset_extend (subset_univ _)
  let hC := Basis.extend this
  haveI Vinh : Inhabited V := ‚ü®0‚ü©
  refine ‚ü®(hC.constr ‚Ñï : _ ‚Üí _) (C.restrict (invFun f)), hB.ext fun b => ?_‚ü©
  rw [image_subset_iff] at BC
  have fb_eq : f b = hC ‚ü®f b, BC b.2‚ü© := by
    change f b = Basis.extend this _
    simp_rw [Basis.extend_apply_self]
  dsimp
  rw [Basis.ofVectorSpace_apply_self, fb_eq, hC.constr_basis]
  exact leftInverse_invFun (LinearMap.ker_eq_bot.1 hf_inj) _

open scoped Classical in
/-- The left inverse of `f : E ‚Üí‚Çó[ùïú] F`.

If `f` is not injective, then we use the junk value `0`. -/
noncomputable
def LinearMap.leftInverse (f : V ‚Üí‚Çó[K] V') : V' ‚Üí‚Çó[K] V :=
  if h_inj : LinearMap.ker f = ‚ä• then
  (f.exists_leftInverse_of_injective h_inj).choose
  else 0

theorem LinearMap.leftInverse_comp_of_inj {f : V ‚Üí‚Çó[K] V'} (h_inj : LinearMap.ker f = ‚ä•) :
    f.leftInverse ‚àò‚Çó f = LinearMap.id := by
  simpa [leftInverse, h_inj] using (f.exists_leftInverse_of_injective h_inj).choose_spec

/-- If `f` is injective, then the left inverse composed with `f` is the identity. -/
theorem LinearMap.leftInverse_apply_of_inj {f : V ‚Üí‚Çó[K] V'} (h_inj : LinearMap.ker f = ‚ä•) (x : V) :
    f.leftInverse (f x) = x :=
  LinearMap.ext_iff.mp (f.leftInverse_comp_of_inj h_inj) x

theorem Submodule.exists_isCompl (p : Submodule K V) : ‚àÉ q : Submodule K V, IsCompl p q :=
  ‚ü®LinearMap.ker p.subtype.leftInverse,
    LinearMap.isCompl_of_proj <| LinearMap.leftInverse_apply_of_inj p.ker_subtype‚ü©

instance Submodule.complementedLattice : ComplementedLattice (Submodule K V) :=
  ‚ü®Submodule.exists_isCompl‚ü©

/-- Any linear map `f : p ‚Üí‚Çó[K] V'` defined on a subspace `p` can be extended to the whole
space. -/
theorem LinearMap.exists_extend {p : Submodule K V} (f : p ‚Üí‚Çó[K] V') :
    ‚àÉ g : V ‚Üí‚Çó[K] V', g.comp p.subtype = f :=
  let ‚ü®g, hg‚ü© := p.subtype.exists_leftInverse_of_injective p.ker_subtype
  ‚ü®f.comp g, by rw [LinearMap.comp_assoc, hg, f.comp_id]‚ü©

theorem LinearMap.exists_extend_of_notMem {p : Submodule K V} {v : V} (f : p ‚Üí‚Çó[K] V')
    (hv : v ‚àâ p) (y : V') : ‚àÉ g : V ‚Üí‚Çó[K] V', g.comp p.subtype = f ‚àß g v = y := by
  rcases (LinearPMap.supSpanSingleton ‚ü®p, f‚ü© v y hv).toFun.exists_extend with ‚ü®g, hg‚ü©
  refine ‚ü®g, ?_, ?_‚ü©
  ¬∑ ext x
    have := LinearPMap.supSpanSingleton_apply_mk_of_mem ‚ü®p, f‚ü© y hv x.2
    simpa using congr($hg _).trans this
  ¬∑ have := LinearPMap.supSpanSingleton_apply_self ‚ü®p, f‚ü© y hv
    simpa using congr($hg _).trans this

open Submodule LinearMap

theorem Submodule.exists_le_ker_of_notMem {p : Submodule K V} {v : V} (hv : v ‚àâ p) :
    ‚àÉ f : V ‚Üí‚Çó[K] K, f v ‚â† 0 ‚àß p ‚â§ ker f := by
  rcases LinearMap.exists_extend_of_notMem (0 : p ‚Üí‚Çó[K] K) hv 1 with ‚ü®f, hpf, hfv‚ü©
  refine ‚ü®f, by simp [hfv], fun x hx ‚Ü¶ ?_‚ü©
  simpa using congr($hpf ‚ü®x, hx‚ü©)

/-- If `V` and `V'` are nontrivial vector spaces over a field `K`, the space of `K`-linear maps
between them is nontrivial. -/
instance [Nontrivial V] [Nontrivial V'] : Nontrivial (V ‚Üí‚Çó[K] V') := by
  obtain ‚ü®v, hv‚ü© := exists_ne (0 : V)
  obtain ‚ü®w, hw‚ü© := exists_ne (0 : V')
  have : v ‚àâ (‚ä• : Submodule K V) := by simp only [mem_bot, hv, not_false_eq_true]
  obtain ‚ü®g, _, hg‚ü© := LinearMap.exists_extend_of_notMem (K := K) 0 this w
  exact ‚ü®g, 0, DFunLike.ne_iff.mpr ‚ü®v, by simp_all‚ü©‚ü©

/-- If `p < ‚ä§` is a subspace of a vector space `V`, then there exists a nonzero linear map
`f : V ‚Üí‚Çó[K] K` such that `p ‚â§ ker f`. -/
theorem Submodule.exists_le_ker_of_lt_top (p : Submodule K V) (hp : p < ‚ä§) :
    ‚àÉ (f : V ‚Üí‚Çó[K] K), f ‚â† 0 ‚àß p ‚â§ ker f := by
  rcases SetLike.exists_of_lt hp with ‚ü®v, -, hpv‚ü©
  rcases exists_le_ker_of_notMem hpv with ‚ü®f, hfv, hpf‚ü©
  exact ‚ü®f, ne_of_apply_ne (¬∑ v) hfv, hpf‚ü©

theorem quotient_prod_linearEquiv (p : Submodule K V) : Nonempty (((V ‚ß∏ p) √ó p) ‚âÉ‚Çó[K] V) :=
  let ‚ü®q, hq‚ü© := p.exists_isCompl
  Nonempty.intro <|
    ((quotientEquivOfIsCompl p q hq).prodCongr (LinearEquiv.refl _ _)).trans
      (prodEquivOfIsCompl q p hq.symm)

end DivisionRing

section Field

open Submodule LinearMap Module

variable {K : Type*} {V : Type*} [Field K] [AddCommGroup V] [Module K V]

variable {f : V ‚Üí‚Çó[K] K} {v : V}

/-- In a vector space, given a nonzero linear form `f`,
a nonzero vector `v` such that `f v ‚â† 0`,
there exists a basis `b` with an index `i`
such that `v = b i` and `f = (f v) ‚Ä¢ b.coord i`. -/
theorem exists_basis_of_pairing_ne_zero
    (hfv : f v ‚â† 0) :
    ‚àÉ (n : Set V) (b : Module.Basis n K V) (i : n),
      v = b i ‚àß f = (f v) ‚Ä¢ b.coord i := by
  set b‚ÇÅ := Basis.ofVectorSpace K (ker f)
  set s : Set V := (ker f).subtype '' Set.range b‚ÇÅ
  have hs : span K s = ker f := by
    simp only [s, span_image]
    simp
  set n := insert v s
  have H‚ÇÅ : LinearIndepOn K _root_.id n := by
    apply LinearIndepOn.id_insert
    ¬∑ apply LinearIndepOn.image
      ¬∑ exact b‚ÇÅ.linearIndependent.linearIndepOn_id
      ¬∑ simp
    ¬∑ simp [hs, hfv]
  have H‚ÇÇ : ‚ä§ ‚â§ span K n := by
    rintro x -
    simp only [n, mem_span_insert']
    use -f x / f v
    simp only [hs, mem_ker, map_add, map_smul, smul_eq_mul]
    field
  set b := Basis.mk H‚ÇÅ (by simpa using H‚ÇÇ)
  set i : n := ‚ü®v, s.mem_insert v‚ü©
  have hi : b i = v := by simp [b, i]
  refine ‚ü®n, b, i, by simp [b, i], ?_‚ü©
  rw [‚Üê hi]
  apply b.ext
  intro j
  by_cases h : i = j
  ¬∑ simp [h]
  ¬∑ suffices f (b j) = 0 by
      simp [Finsupp.single_eq_of_ne h, this]
    rw [‚Üê mem_ker, ‚Üê hs, Basis.coe_mk]
    apply subset_span
    apply Or.resolve_left (Set.mem_insert_iff.mpr j.prop)
    simp [‚Üê hi, b, Subtype.coe_inj, Ne.symm h]

/-- In a vector space, given a nonzero linear form `f`,
a nonzero vector `v` such that `f v = 0`,
there exists a basis `b` with two distinct indices `i`, `j`
such that `v = b i` and `f = b.coord j`. -/
theorem exists_basis_of_pairing_eq_zero
    (hfv : f v = 0) (hf : f ‚â† 0) (hv : v ‚â† 0) :
    ‚àÉ (n : Set V) (b : Basis n K V) (i j : n),
      i ‚â† j ‚àß v = b i ‚àß f = b.coord j := by
  lift v to ker f using hfv
  have : LinearIndepOn K _root_.id {v} := by simpa using hv
  set b‚ÇÅ : Basis _ K (ker f) := .extend this
  obtain ‚ü®w, hw‚ü© : ‚àÉ w, f w = 1 := by
    simp only [ne_eq, DFunLike.ext_iff, not_forall] at hf
    rcases hf with ‚ü®w, hw‚ü©
    use (f w)‚Åª¬π ‚Ä¢ w
    simp_all
  set s : Set V := (ker f).subtype '' Set.range b‚ÇÅ
  have hs : span K s = ker f := by
    simp only [s, span_image]
    simp
  have hvs : ‚Üëv ‚àà s := by
    refine ‚ü®v, ?_, by simp‚ü©
    simp [b‚ÇÅ, this.subset_extend _ _]
  set n := insert w s
  have H‚ÇÅ : LinearIndepOn K _root_.id n := by
    apply LinearIndepOn.id_insert
    ¬∑ apply LinearIndepOn.image
      ¬∑ exact b‚ÇÅ.linearIndependent.linearIndepOn_id
      ¬∑ simp
    ¬∑ simp [hs, hw]
  have H‚ÇÇ : ‚ä§ ‚â§ span K n := by
    rintro x -
    simp only [n, mem_span_insert']
    use -f x
    simp [hs, hw]
  set b := Basis.mk H‚ÇÅ (by simpa using H‚ÇÇ)
  refine ‚ü®n, b, ‚ü®v, by simp [n, hvs]‚ü©, ‚ü®w, by simp [n]‚ü©, ?_, by simp [b], ?_‚ü©
  ¬∑ apply_fun (f ‚àò (‚Üë))
    simp [hw]
  ¬∑ apply b.ext
    intro i
    rw [Basis.coord_apply, Basis.repr_self]
    simp only [b, Basis.mk_apply]
    rcases i with ‚ü®x, rfl | ‚ü®x, hx, rfl‚ü©‚ü©
    ¬∑ simp [hw]
    ¬∑ suffices x ‚â† w by simp [this]
      apply_fun f
      simp [hw]

end Field
