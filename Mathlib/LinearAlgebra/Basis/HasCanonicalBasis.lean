/-
Copyright (c) 2025 Paul Lezeau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul Lezeau, Eric Wieser
-/
import Mathlib.Analysis.InnerProductSpace.PiL2

/-! # Canonical Bases for vector spaces

This file introduces the notion of canonical bases for modules,
which allows us to work with an implicit notion of the canonical basis for familiar spaces.

-/

universe u v w u' v' w'

/-- `HasCanonicalBasis ğ•œ V Î¹ f` means that the `ğ•œ`-module `V` is canonically
represented by an `Î¹`-indexed basis with elements `f`.

`f` is an outParam so that theorems about a particular basis can use the simp-normal `Pi.single`
rather than `Pi.basisFun`. -/
class HasCanonicalBasis (ğ•œ : Type u) (V : Type v) (Î¹ : outParam <| Type w)
    --TODO: can some of these be `semiOutParam`/regular params?
    (f : outParam <| Î¹ â†’ V) [Semiring ğ•œ] [AddCommGroup V]
    [Module ğ•œ V] where
  /-- The underlying basis -/
  basis : Basis Î¹ ğ•œ V
  coe_basis_eq : basis = f

namespace HasCanonicalBasis

@[simp]
lemma basis_apply (ğ•œ : Type u) (V : Type v) (Î¹ : outParam <| Type w)
    (f : outParam <| Î¹ â†’ V) [Semiring ğ•œ] [AddCommGroup V]
    [Module ğ•œ V] [hc : HasCanonicalBasis ğ•œ V Î¹ f] (i : Î¹) :
    hc.basis i = f i := by
  simp [coe_basis_eq]

end HasCanonicalBasis

variable {ğ•œ : Type u} {Î¹ : Type w} [DecidableEq Î¹] [Fintype Î¹]

noncomputable instance EuclideanSpace.hasCanonicalBasis [RCLike ğ•œ] :
    HasCanonicalBasis ğ•œ (EuclideanSpace ğ•œ Î¹) Î¹ (EuclideanSpace.single Â· 1) where
  basis := PiLp.basisFun 2 ğ•œ Î¹
  coe_basis_eq := by ext : 1; simp

variable [Ring ğ•œ]

noncomputable instance Pi.hasCanonicalBasis : HasCanonicalBasis ğ•œ (Î¹ â†’ ğ•œ) Î¹ (Pi.single Â· 1) where
  basis := Pi.basisFun ğ•œ Î¹
  coe_basis_eq := by ext; simp

/-
Note: this could be generalised to a product of vector spaces that each have a
`HasCanonicalBasis` instance, but for now this isn't necessary, and the index
type would be a very ugly type, which is undesirable.
-/
noncomputable instance [Ring ğ•œ] (p : ENNReal) :
  HasCanonicalBasis ğ•œ (PiLp p (fun (_ : Î¹) â†¦ ğ•œ)) Î¹ (Pi.single Â· 1) where
  basis := (PiLp.basisFun p ğ•œ Î¹)
  coe_basis_eq := by ext; simp

noncomputable instance : HasCanonicalBasis ğ•œ ğ•œ (Fin 1) (fun _ â†¦ 1) where
  basis := Basis.singleton _ ğ•œ
  coe_basis_eq := by ext i; aesop

/-- This abbrev provides us with a way of reindexing canonical bases, which is useful
in the context of defining canonical bases for products. -/
noncomputable abbrev reindex {V : Type v} {Îº : Type w'}
    {f : Î¹ â†’ V} {g : Îº â†’ V} [Semiring ğ•œ] [AddCommGroup V] [Module ğ•œ V]
    (hc : HasCanonicalBasis ğ•œ V Î¹ f) (e : Î¹ â‰ƒ Îº) (he : âˆ€ (i : Îº), g i = hc.basis (e.symm i)) :
    HasCanonicalBasis ğ•œ V Îº g where
  basis := Basis.reindex (HasCanonicalBasis.basis) e
  coe_basis_eq := by ext; simp [Basis.reindex_apply, he]

variable (ğ•œ) in
/-- Constructs a "canonical basis" on a product of two modules equipped with a canonical basis.
This isn't an instance since have a sum as the index type for our bases is in general undesirable
(e.g. this would force `ğ•œ Ã— ğ•œ`  to have basis `Fin 1 âŠ• Fin 1` rather than `Fin 2`) -/
noncomputable abbrev prod {V : Type v} {W : Type v'} {Îº : Type w'}
    [AddCommGroup V] [AddCommGroup W] [Module ğ•œ V] [Module ğ•œ W]
    (f : Î¹ â†’ V) (g : Îº â†’ W) [HasCanonicalBasis ğ•œ V Î¹ f] [HasCanonicalBasis ğ•œ W Îº g] :
    HasCanonicalBasis ğ•œ (V Ã— W) (Î¹ âŠ• Îº) (Sum.elim (LinearMap.inl ğ•œ _ _ âˆ˜ f)
      (LinearMap.inr ğ•œ _ _ âˆ˜ g)) where
  basis := Basis.prod HasCanonicalBasis.basis HasCanonicalBasis.basis
  coe_basis_eq := by ext <;> simp [Basis.prod_apply, Sum.elim]

/--
The canonical basis for `ğ•œ Ã— ğ•œ`
-/
noncomputable instance : HasCanonicalBasis ğ•œ (ğ•œ Ã— ğ•œ) (Fin 2) (![(1, 0), (0, 1)]) :=
  reindex (prod ğ•œ _ _) finSumFinEquiv
    (fun i â†¦  by fin_cases i <;> simp [finSumFinEquiv, prod, Sum.elim, Fin.addCases])

/--
The canonical basis for `ğ•œ Ã— ğ•œ Ã— ğ•œ`.
-/
noncomputable instance : HasCanonicalBasis ğ•œ (ğ•œ Ã— ğ•œ Ã— ğ•œ) (Fin 3)
    (![(1, 0, 0), (0, 1, 0), (0, 0, 1)]) :=
  reindex (prod ğ•œ _ _) finSumFinEquiv
    (fun i â†¦  by fin_cases i <;> simp [finSumFinEquiv, prod, Sum.elim, Fin.addCases])

-- TODO: maybe we also want to manually construct an instance on `ğ•œ Ã— ğ•œ Ã— ğ•œ Ã— ğ•œ`
