/-
Copyright (c) 2023 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Patrick Massot
-/
import Mathlib.LinearAlgebra.Basis
import Mathlib.Data.Fin.FlagRange

/-!
# Flag of submodules defined by a basis

In this file we define `Basis.flag b k`, where `b : Basis (Fin n) R M`, `k : Fin (n + 1)`,
to be the subspace spanned by the first `k` vectors of the basis `b`.

We also prove some lemmas about this definition.
-/

open Set Submodule

namespace Basis

section Semiring

variable {R M : Type _} [Semiring R] [AddCommMonoid M] [Module R M] {n : â„•}

/-- The subspace spanned by the first `k` vectors of the basis `b`. -/
def flag (b : Basis (Fin n) R M) (k : Fin (n + 1)) : Submodule R M :=
  .span R <| b '' {i | i.castSucc < k}

@[simp]
theorem flag_zero (b : Basis (Fin n) R M) : b.flag 0 = âŠ¥ := by simp [flag]
                                                               -- ðŸŽ‰ no goals

@[simp]
theorem flag_last (b : Basis (Fin n) R M) : b.flag (.last n) = âŠ¤ := by
  simp [flag, Fin.castSucc_lt_last]
  -- ðŸŽ‰ no goals

theorem flag_le_iff (b : Basis (Fin n) R M) {k p} :
    b.flag k â‰¤ p â†” âˆ€ i : Fin n, i.castSucc < k â†’ b i âˆˆ p :=
  span_le.trans ball_image_iff

theorem flag_succ (b : Basis (Fin n) R M) (k : Fin n) :
    b.flag k.succ = (R âˆ™ b k) âŠ” b.flag k.castSucc := by
  simp only [flag, Fin.castSucc_lt_castSucc_iff]
  -- âŠ¢ span R ((fun a => â†‘b a) '' {i | Fin.castSucc i < Fin.succ k}) = span R {â†‘b k â€¦
  simp [Fin.castSucc_lt_iff_succ_le, le_iff_eq_or_lt, setOf_or, image_insert_eq, span_insert]
  -- ðŸŽ‰ no goals

theorem self_mem_flag (b : Basis (Fin n) R M) {i : Fin n} {k : Fin (n + 1)} (h : i.castSucc < k) :
    b i âˆˆ b.flag k :=
  subset_span <| mem_image_of_mem _ h

@[simp]
theorem self_mem_flag_iff [Nontrivial R] (b : Basis (Fin n) R M) {i : Fin n} {k : Fin (n + 1)} :
    b i âˆˆ b.flag k â†” i.castSucc < k :=
  b.self_mem_span_image

@[mono]
theorem flag_mono (b : Basis (Fin n) R M) : Monotone b.flag :=
  Fin.monotone_iff_le_succ.2 fun k â†¦ by rw [flag_succ]; exact le_sup_right
                                        -- âŠ¢ flag b (Fin.castSucc k) â‰¤ span R {â†‘b k} âŠ” flag b (Fin.castSucc k)
                                                        -- ðŸŽ‰ no goals

theorem isChain_range_flag (b : Basis (Fin n) R M) : IsChain (Â· â‰¤ Â·) (range b.flag) :=
  b.flag_mono.isChain_range

@[mono]
theorem flag_strictMono [Nontrivial R] (b : Basis (Fin n) R M) : StrictMono b.flag :=
  Fin.strictMono_iff_lt_succ.2 fun _ â†¦ by simp [flag_succ]
                                          -- ðŸŽ‰ no goals

end Semiring

section CommRing

variable {R M : Type _} [CommRing R] [AddCommGroup M] [Module R M] {n : â„•}

@[simp]
theorem flag_le_ker_coord_iff [Nontrivial R] (b : Basis (Fin n) R M) {k : Fin (n + 1)} {l : Fin n} :
    b.flag k â‰¤ LinearMap.ker (b.coord l) â†” k â‰¤ l.castSucc := by
  simp [flag_le_iff, Finsupp.single_apply_eq_zero, imp_false, imp_not_comm]
  -- ðŸŽ‰ no goals

theorem flag_le_ker_coord (b : Basis (Fin n) R M) {k : Fin (n + 1)} {l : Fin n}
    (h : k â‰¤ l.castSucc) : b.flag k â‰¤ LinearMap.ker (b.coord l) := by
  nontriviality R
  -- âŠ¢ flag b k â‰¤ LinearMap.ker (coord b l)
  exact b.flag_le_ker_coord_iff.2 h
  -- ðŸŽ‰ no goals

end CommRing

section DivisionRing

variable {K V : Type _} [DivisionRing K] [AddCommGroup V] [Module K V] {n : â„•}

theorem flag_covby (b : Basis (Fin n) K V) (i : Fin n) :
    b.flag i.castSucc â‹– b.flag i.succ := by
  rw [flag_succ]
  -- âŠ¢ flag b (Fin.castSucc i) â‹– span K {â†‘b i} âŠ” flag b (Fin.castSucc i)
  apply covby_span_singleton_sup
  -- âŠ¢ Â¬â†‘b i âˆˆ flag b (Fin.castSucc i)
  simp
  -- ðŸŽ‰ no goals

theorem flag_wcovby (b : Basis (Fin n) K V) (i : Fin n) :
    b.flag i.castSucc â©¿ b.flag i.succ :=
  (b.flag_covby i).wcovby

/-- Range of `Basis.flag` as a `Flag`. -/
@[simps!]
def toFlag (b : Basis (Fin n) K V) : Flag (Submodule K V) :=
  .rangeFin b.flag b.flag_zero b.flag_last b.flag_wcovby

@[simp]
theorem mem_toFlag (b : Basis (Fin n) K V) {p : Submodule K V} : p âˆˆ b.toFlag â†” âˆƒ k, b.flag k = p :=
  Iff.rfl

theorem isMaxChain_range_flag (b : Basis (Fin n) K V) : IsMaxChain (Â· â‰¤ Â·) (range b.flag) :=
  b.toFlag.maxChain

end DivisionRing
