/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Kevin Buzzard, Yury Kudryashov
-/
import Mathlib.GroupTheory.QuotientGroup
import Mathlib.LinearAlgebra.Span

#align_import linear_algebra.quotient from "leanprover-community/mathlib"@"48085f140e684306f9e7da907cd5932056d1aded"

/-!
# Quotients by submodules

* If `p` is a submodule of `M`, `M â§¸ p` is the quotient of `M` with respect to `p`:
  that is, elements of `M` are identified if their difference is in `p`. This is itself a module.

-/

-- For most of this file we work over a noncommutative ring
section Ring

namespace Submodule

variable {R M : Type*} {r : R} {x y : M} [Ring R] [AddCommGroup M] [Module R M]

variable (p p' : Submodule R M)

open LinearMap QuotientAddGroup

/-- The equivalence relation associated to a submodule `p`, defined by `x â‰ˆ y` iff `-x + y âˆˆ p`.

Note this is equivalent to `y - x âˆˆ p`, but defined this way to be defeq to the `AddSubgroup`
version, where commutativity can't be assumed. -/
def quotientRel : Setoid M :=
  QuotientAddGroup.leftRel p.toAddSubgroup
#align submodule.quotient_rel Submodule.quotientRel

theorem quotientRel_r_def {x y : M} : @Setoid.r _ p.quotientRel x y â†” x - y âˆˆ p :=
  Iff.trans
    (by
      rw [leftRel_apply, sub_eq_add_neg, neg_add, neg_neg]
      -- âŠ¢ -x + y âˆˆ toAddSubgroup p â†” -x + y âˆˆ p
      rfl)
      -- ğŸ‰ no goals
    neg_mem_iff
#align submodule.quotient_rel_r_def Submodule.quotientRel_r_def

/-- The quotient of a module `M` by a submodule `p âŠ† M`. -/
instance hasQuotient : HasQuotient M (Submodule R M) :=
  âŸ¨fun p => Quotient (quotientRel p)âŸ©
#align submodule.has_quotient Submodule.hasQuotient

namespace Quotient

/-- Map associating to an element of `M` the corresponding element of `M/p`,
when `p` is a submodule of `M`. -/
def mk {p : Submodule R M} : M â†’ M â§¸ p :=
  Quotient.mk''
#align submodule.quotient.mk Submodule.Quotient.mk

/- porting note: here and throughout elaboration is sped up *tremendously* (in some cases even
avoiding timeouts) by providing type ascriptions to `mk` (or `mk x`) and its variants. Lean 3
didn't need this help. -/
@[simp]
theorem mk'_eq_mk' {p : Submodule R M} (x : M) :
    @Quotient.mk' _ (quotientRel p) x = (mk : M â†’ M â§¸ p) x :=
  rfl
#align submodule.quotient.mk_eq_mk Submodule.Quotient.mk'_eq_mk'

@[simp]
theorem mk''_eq_mk {p : Submodule R M} (x : M) : (Quotient.mk'' x : M â§¸ p) = (mk : M â†’ M â§¸ p) x :=
  rfl
#align submodule.quotient.mk'_eq_mk Submodule.Quotient.mk''_eq_mk

@[simp]
theorem quot_mk_eq_mk {p : Submodule R M} (x : M) : (Quot.mk _ x : M â§¸ p) = (mk : M â†’ M â§¸ p) x :=
  rfl
#align submodule.quotient.quot_mk_eq_mk Submodule.Quotient.quot_mk_eq_mk

protected theorem eq' {x y : M} : (mk x : M â§¸ p) = (mk : M â†’ M â§¸ p) y â†” -x + y âˆˆ p :=
  QuotientAddGroup.eq
#align submodule.quotient.eq' Submodule.Quotient.eq'

protected theorem eq {x y : M} : (mk x : M â§¸ p) = (mk y : M â§¸ p) â†” x - y âˆˆ p :=
  (Submodule.Quotient.eq' p).trans (leftRel_apply.symm.trans p.quotientRel_r_def)
#align submodule.quotient.eq Submodule.Quotient.eq

instance : Zero (M â§¸ p) where
  -- Use Quotient.mk'' instead of mk here because mk is not reducible.
  -- This would lead to non-defeq diamonds.
  -- See also the same comment at the One instance for Con.
  zero := Quotient.mk'' 0

instance : Inhabited (M â§¸ p) :=
  âŸ¨0âŸ©

@[simp]
theorem mk_zero : mk 0 = (0 : M â§¸ p) :=
  rfl
#align submodule.quotient.mk_zero Submodule.Quotient.mk_zero

@[simp]
theorem mk_eq_zero : (mk x : M â§¸ p) = 0 â†” x âˆˆ p := by simpa using (Quotient.eq' p : mk x = 0 â†” _)
                                                      -- ğŸ‰ no goals
#align submodule.quotient.mk_eq_zero Submodule.Quotient.mk_eq_zero

instance addCommGroup : AddCommGroup (M â§¸ p) :=
  QuotientAddGroup.Quotient.addCommGroup p.toAddSubgroup
#align submodule.quotient.add_comm_group Submodule.Quotient.addCommGroup

@[simp]
theorem mk_add : (mk (x + y) : M â§¸ p) = (mk x : M â§¸ p) + (mk y : M â§¸ p) :=
  rfl
#align submodule.quotient.mk_add Submodule.Quotient.mk_add

@[simp]
theorem mk_neg : (mk (-x) : M â§¸ p) = -(mk x : M â§¸ p) :=
  rfl
#align submodule.quotient.mk_neg Submodule.Quotient.mk_neg

@[simp]
theorem mk_sub : (mk (x - y) : M â§¸ p) = (mk x : M â§¸ p) - (mk y : M â§¸ p) :=
  rfl
#align submodule.quotient.mk_sub Submodule.Quotient.mk_sub

section SMul

variable {S : Type*} [SMul S R] [SMul S M] [IsScalarTower S R M] (P : Submodule R M)

instance instSMul' : SMul S (M â§¸ P) :=
  âŸ¨fun a =>
    Quotient.map' ((Â· â€¢ Â·) a) fun x y h =>
      leftRel_apply.mpr <| by simpa using Submodule.smul_mem P (a â€¢ (1 : R)) (leftRel_apply.mp h)âŸ©
                              -- ğŸ‰ no goals
#align submodule.quotient.has_smul' Submodule.Quotient.instSMul'

-- porting note: should this be marked as a `@[default_instance]`?
/-- Shortcut to help the elaborator in the common case. -/
instance instSMul : SMul R (M â§¸ P) :=
  Quotient.instSMul' P
#align submodule.quotient.has_smul Submodule.Quotient.instSMul

@[simp]
theorem mk_smul (r : S) (x : M) : (mk (r â€¢ x) : M â§¸ p) = r â€¢ mk x :=
  rfl
#align submodule.quotient.mk_smul Submodule.Quotient.mk_smul

instance smulCommClass (T : Type*) [SMul T R] [SMul T M] [IsScalarTower T R M]
    [SMulCommClass S T M] : SMulCommClass S T (M â§¸ P)
    where smul_comm _x _y := Quotient.ind' fun _z => congr_arg mk (smul_comm _ _ _)
#align submodule.quotient.smul_comm_class Submodule.Quotient.smulCommClass

instance isScalarTower (T : Type*) [SMul T R] [SMul T M] [IsScalarTower T R M] [SMul S T]
    [IsScalarTower S T M] : IsScalarTower S T (M â§¸ P)
    where smul_assoc _x _y := Quotient.ind' fun _z => congr_arg mk (smul_assoc _ _ _)
#align submodule.quotient.is_scalar_tower Submodule.Quotient.isScalarTower

instance isCentralScalar [SMul Sáµáµ’áµ– R] [SMul Sáµáµ’áµ– M] [IsScalarTower Sáµáµ’áµ– R M]
    [IsCentralScalar S M] : IsCentralScalar S (M â§¸ P)
    where op_smul_eq_smul _x := Quotient.ind' fun _z => congr_arg mk <| op_smul_eq_smul _ _
#align submodule.quotient.is_central_scalar Submodule.Quotient.isCentralScalar

end SMul

section Module

variable {S : Type*}

instance mulAction' [Monoid S] [SMul S R] [MulAction S M] [IsScalarTower S R M]
    (P : Submodule R M) : MulAction S (M â§¸ P) :=
  Function.Surjective.mulAction mk (surjective_quot_mk _) <| Submodule.Quotient.mk_smul P
#align submodule.quotient.mul_action' Submodule.Quotient.mulAction'

-- porting note: should this be marked as a `@[default_instance]`?
instance mulAction (P : Submodule R M) : MulAction R (M â§¸ P) :=
  Quotient.mulAction' P
#align submodule.quotient.mul_action Submodule.Quotient.mulAction

instance smulZeroClass' [SMul S R] [SMulZeroClass S M] [IsScalarTower S R M] (P : Submodule R M) :
    SMulZeroClass S (M â§¸ P) :=
  ZeroHom.smulZeroClass âŸ¨mk, mk_zero _âŸ© <| Submodule.Quotient.mk_smul P
#align submodule.quotient.smul_zero_class' Submodule.Quotient.smulZeroClass'

-- porting note: should this be marked as a `@[default_instance]`?
instance smulZeroClass (P : Submodule R M) : SMulZeroClass R (M â§¸ P) :=
  Quotient.smulZeroClass' P
#align submodule.quotient.smul_zero_class Submodule.Quotient.smulZeroClass

instance distribSMul' [SMul S R] [DistribSMul S M] [IsScalarTower S R M] (P : Submodule R M) :
    DistribSMul S (M â§¸ P) :=
  Function.Surjective.distribSMul {toFun := mk, map_zero' := rfl, map_add' := fun _ _ => rfl}
    (surjective_quot_mk _) (Submodule.Quotient.mk_smul P)
#align submodule.quotient.distrib_smul' Submodule.Quotient.distribSMul'

-- porting note: should this be marked as a `@[default_instance]`?
instance distribSMul (P : Submodule R M) : DistribSMul R (M â§¸ P) :=
  Quotient.distribSMul' P
#align submodule.quotient.distrib_smul Submodule.Quotient.distribSMul

instance distribMulAction' [Monoid S] [SMul S R] [DistribMulAction S M] [IsScalarTower S R M]
    (P : Submodule R M) : DistribMulAction S (M â§¸ P) :=
  Function.Surjective.distribMulAction {toFun := mk, map_zero' := rfl, map_add' := fun _ _ => rfl}
    (surjective_quot_mk _) (Submodule.Quotient.mk_smul P)
#align submodule.quotient.distrib_mul_action' Submodule.Quotient.distribMulAction'

-- porting note: should this be marked as a `@[default_instance]`?
instance distribMulAction (P : Submodule R M) : DistribMulAction R (M â§¸ P) :=
  Quotient.distribMulAction' P
#align submodule.quotient.distrib_mul_action Submodule.Quotient.distribMulAction

instance module' [Semiring S] [SMul S R] [Module S M] [IsScalarTower S R M] (P : Submodule R M) :
    Module S (M â§¸ P) :=
  Function.Surjective.module _ {toFun := mk, map_zero' := rfl, map_add' := fun _ _ => rfl}
    (surjective_quot_mk _) (Submodule.Quotient.mk_smul P)
#align submodule.quotient.module' Submodule.Quotient.module'

-- porting note: should this be marked as a `@[default_instance]`?
instance module (P : Submodule R M) : Module R (M â§¸ P) :=
  Quotient.module' P
#align submodule.quotient.module Submodule.Quotient.module

variable (S)

/-- The quotient of `P` as an `S`-submodule is the same as the quotient of `P` as an `R`-submodule,
where `P : Submodule R M`.
-/
def restrictScalarsEquiv [Ring S] [SMul S R] [Module S M] [IsScalarTower S R M]
    (P : Submodule R M) : (M â§¸ P.restrictScalars S) â‰ƒâ‚—[S] M â§¸ P :=
  { Quotient.congrRight fun _ _ => Iff.rfl with
    map_add' := fun x y => Quotient.inductionOnâ‚‚' x y fun _x' _y' => rfl
    map_smul' := fun _c x => Quotient.inductionOn' x fun _x' => rfl }
#align submodule.quotient.restrict_scalars_equiv Submodule.Quotient.restrictScalarsEquiv

@[simp]
theorem restrictScalarsEquiv_mk [Ring S] [SMul S R] [Module S M] [IsScalarTower S R M]
    (P : Submodule R M) (x : M) :
    restrictScalarsEquiv S P (mk x : M â§¸ P) = (mk x : M â§¸ P) :=
  rfl
#align submodule.quotient.restrict_scalars_equiv_mk Submodule.Quotient.restrictScalarsEquiv_mk

@[simp]
theorem restrictScalarsEquiv_symm_mk [Ring S] [SMul S R] [Module S M] [IsScalarTower S R M]
    (P : Submodule R M) (x : M) :
    (restrictScalarsEquiv S P).symm ((mk : M â†’ M â§¸ P) x) = (mk : M â†’ M â§¸ P) x :=
  rfl
#align submodule.quotient.restrict_scalars_equiv_symm_mk Submodule.Quotient.restrictScalarsEquiv_symm_mk

end Module

theorem mk_surjective : Function.Surjective (@mk _ _ _ _ _ p) := by
  rintro âŸ¨xâŸ©
  -- âŠ¢ âˆƒ a, mk a = Quot.mk Setoid.r x
  exact âŸ¨x, rflâŸ©
  -- ğŸ‰ no goals
#align submodule.quotient.mk_surjective Submodule.Quotient.mk_surjective

theorem nontrivial_of_lt_top (h : p < âŠ¤) : Nontrivial (M â§¸ p) := by
  obtain âŸ¨x, _, not_mem_sâŸ© := SetLike.exists_of_lt h
  -- âŠ¢ Nontrivial (M â§¸ p)
  refine' âŸ¨âŸ¨mk x, 0, _âŸ©âŸ©
  -- âŠ¢ mk x â‰  0
  simpa using not_mem_s
  -- ğŸ‰ no goals
#align submodule.quotient.nontrivial_of_lt_top Submodule.Quotient.nontrivial_of_lt_top

end Quotient

instance QuotientBot.infinite [Infinite M] : Infinite (M â§¸ (âŠ¥ : Submodule R M)) :=
  Infinite.of_injective Submodule.Quotient.mk fun _x _y h =>
    sub_eq_zero.mp <| (Submodule.Quotient.eq âŠ¥).mp h
#align submodule.quotient_bot.infinite Submodule.QuotientBot.infinite

instance QuotientTop.unique : Unique (M â§¸ (âŠ¤ : Submodule R M)) where
  default := 0
  uniq x := Quotient.inductionOn' x fun _x => (Submodule.Quotient.eq âŠ¤).mpr Submodule.mem_top
#align submodule.quotient_top.unique Submodule.QuotientTop.unique

instance QuotientTop.fintype : Fintype (M â§¸ (âŠ¤ : Submodule R M)) :=
  Fintype.ofSubsingleton 0
#align submodule.quotient_top.fintype Submodule.QuotientTop.fintype

variable {p}

theorem subsingleton_quotient_iff_eq_top : Subsingleton (M â§¸ p) â†” p = âŠ¤ := by
  constructor
  -- âŠ¢ Subsingleton (M â§¸ p) â†’ p = âŠ¤
  Â· rintro h
    -- âŠ¢ p = âŠ¤
    refine' eq_top_iff.mpr fun x _ => _
    -- âŠ¢ x âˆˆ p
    have : x - 0 âˆˆ p := (Submodule.Quotient.eq p).mp (Subsingleton.elim _ _)
    -- âŠ¢ x âˆˆ p
    rwa [sub_zero] at this
    -- ğŸ‰ no goals
  Â· rintro rfl
    -- âŠ¢ Subsingleton (M â§¸ âŠ¤)
    infer_instance
    -- ğŸ‰ no goals
#align submodule.subsingleton_quotient_iff_eq_top Submodule.subsingleton_quotient_iff_eq_top

theorem unique_quotient_iff_eq_top : Nonempty (Unique (M â§¸ p)) â†” p = âŠ¤ :=
  âŸ¨fun âŸ¨hâŸ© => subsingleton_quotient_iff_eq_top.mp (@Unique.instSubsingleton _ h),
    by rintro rfl; exact âŸ¨QuotientTop.uniqueâŸ©âŸ©
       -- âŠ¢ Nonempty (Unique (M â§¸ âŠ¤))
                   -- ğŸ‰ no goals
#align submodule.unique_quotient_iff_eq_top Submodule.unique_quotient_iff_eq_top

variable (p)

noncomputable instance Quotient.fintype [Fintype M] (S : Submodule R M) : Fintype (M â§¸ S) :=
  @_root_.Quotient.fintype _ _ _ fun _ _ => Classical.dec _
#align submodule.quotient.fintype Submodule.Quotient.fintype

theorem card_eq_card_quotient_mul_card [Fintype M] (S : Submodule R M) [DecidablePred (Â· âˆˆ S)] :
    Fintype.card M = Fintype.card S * Fintype.card (M â§¸ S) := by
  rw [mul_comm, â† Fintype.card_prod]
  -- âŠ¢ Fintype.card M = Fintype.card ((M â§¸ S) Ã— { x // x âˆˆ S })
  exact Fintype.card_congr AddSubgroup.addGroupEquivQuotientProdAddSubgroup
  -- ğŸ‰ no goals
#align submodule.card_eq_card_quotient_mul_card Submodule.card_eq_card_quotient_mul_card

section

variable {Mâ‚‚ : Type*} [AddCommGroup Mâ‚‚] [Module R Mâ‚‚]

theorem quot_hom_ext (f g : (M â§¸ p) â†’â‚—[R] Mâ‚‚) (h : âˆ€ x : M, f (Quotient.mk x) = g (Quotient.mk x)) :
    f = g :=
  LinearMap.ext fun x => Quotient.inductionOn' x h
#align submodule.quot_hom_ext Submodule.quot_hom_ext

/-- The map from a module `M` to the quotient of `M` by a submodule `p` as a linear map. -/
def mkQ : M â†’â‚—[R] M â§¸ p where
  toFun := Quotient.mk
  map_add' := by simp
                 -- ğŸ‰ no goals
  map_smul' := by simp
                  -- ğŸ‰ no goals
#align submodule.mkq Submodule.mkQ

@[simp]
theorem mkQ_apply (x : M) : p.mkQ x = (Quotient.mk x : M â§¸ p) :=
  rfl
#align submodule.mkq_apply Submodule.mkQ_apply

theorem mkQ_surjective (A : Submodule R M) : Function.Surjective A.mkQ := by
  rintro âŸ¨xâŸ©; exact âŸ¨x, rflâŸ©
  -- âŠ¢ âˆƒ a, â†‘(mkQ A) a = Quot.mk Setoid.r x
              -- ğŸ‰ no goals
#align submodule.mkq_surjective Submodule.mkQ_surjective

end

variable {Râ‚‚ Mâ‚‚ : Type*} [Ring Râ‚‚] [AddCommGroup Mâ‚‚] [Module Râ‚‚ Mâ‚‚] {Ï„â‚â‚‚ : R â†’+* Râ‚‚}

/-- Two `LinearMap`s from a quotient module are equal if their compositions with
`submodule.mkQ` are equal.

See note [partially-applied ext lemmas]. -/
@[ext 1100] -- porting note: increase priority so this applies before `LinearMap.ext`
theorem linearMap_qext â¦ƒf g : M â§¸ p â†’â‚›â‚—[Ï„â‚â‚‚] Mâ‚‚â¦„ (h : f.comp p.mkQ = g.comp p.mkQ) : f = g :=
  LinearMap.ext fun x => Quotient.inductionOn' x <| (LinearMap.congr_fun h : _)
#align submodule.linear_map_qext Submodule.linearMap_qext

/-- The map from the quotient of `M` by a submodule `p` to `Mâ‚‚` induced by a linear map `f : M â†’ Mâ‚‚`
vanishing on `p`, as a linear map. -/
def liftQ (f : M â†’â‚›â‚—[Ï„â‚â‚‚] Mâ‚‚) (h : p â‰¤ ker f) : M â§¸ p â†’â‚›â‚—[Ï„â‚â‚‚] Mâ‚‚ :=
  { QuotientAddGroup.lift p.toAddSubgroup f.toAddMonoidHom h with
    map_smul' := by rintro a âŸ¨xâŸ©; exact f.map_smulâ‚›â‚— a x }
                    -- âŠ¢ AddHom.toFun { toFun := srcâœ.toFun, map_add' := (_ : âˆ€ (x y : M â§¸ toAddSubgr â€¦
                                  -- ğŸ‰ no goals
#align submodule.liftq Submodule.liftQ

@[simp]
theorem liftQ_apply (f : M â†’â‚›â‚—[Ï„â‚â‚‚] Mâ‚‚) {h} (x : M) : p.liftQ f h (Quotient.mk x) = f x :=
  rfl
#align submodule.liftq_apply Submodule.liftQ_apply

@[simp]
theorem liftQ_mkQ (f : M â†’â‚›â‚—[Ï„â‚â‚‚] Mâ‚‚) (h) : (p.liftQ f h).comp p.mkQ = f := by ext; rfl
                                                                               -- âŠ¢ â†‘(comp (liftQ p f h) (mkQ p)) xâœ = â†‘f xâœ
                                                                                    -- ğŸ‰ no goals
#align submodule.liftq_mkq Submodule.liftQ_mkQ

/-- Special case of `submodule.liftQ` when `p` is the span of `x`. In this case, the condition on
`f` simply becomes vanishing at `x`.-/
def liftQSpanSingleton (x : M) (f : M â†’â‚›â‚—[Ï„â‚â‚‚] Mâ‚‚) (h : f x = 0) : (M â§¸ R âˆ™ x) â†’â‚›â‚—[Ï„â‚â‚‚] Mâ‚‚ :=
  (R âˆ™ x).liftQ f <| by rw [span_singleton_le_iff_mem, LinearMap.mem_ker, h]
                        -- ğŸ‰ no goals
#align submodule.liftq_span_singleton Submodule.liftQSpanSingleton

@[simp]
theorem liftQSpanSingleton_apply (x : M) (f : M â†’â‚›â‚—[Ï„â‚â‚‚] Mâ‚‚) (h : f x = 0) (y : M) :
    liftQSpanSingleton x f h (Quotient.mk y) = f y :=
  rfl
#align submodule.liftq_span_singleton_apply Submodule.liftQSpanSingleton_apply

@[simp]
theorem range_mkQ : range p.mkQ = âŠ¤ :=
  eq_top_iff'.2 <| by rintro âŸ¨xâŸ©; exact âŸ¨x, rflâŸ©
                      -- âŠ¢ Quot.mk Setoid.r x âˆˆ range (mkQ p)
                                  -- ğŸ‰ no goals
#align submodule.range_mkq Submodule.range_mkQ

@[simp]
theorem ker_mkQ : ker p.mkQ = p := by ext; simp
                                      -- âŠ¢ xâœ âˆˆ ker (mkQ p) â†” xâœ âˆˆ p
                                           -- ğŸ‰ no goals
#align submodule.ker_mkq Submodule.ker_mkQ

theorem le_comap_mkQ (p' : Submodule R (M â§¸ p)) : p â‰¤ comap p.mkQ p' := by
  simpa using (comap_mono bot_le : ker p.mkQ â‰¤ comap p.mkQ p')
  -- ğŸ‰ no goals
#align submodule.le_comap_mkq Submodule.le_comap_mkQ

@[simp]
theorem mkQ_map_self : map p.mkQ p = âŠ¥ := by
  rw [eq_bot_iff, map_le_iff_le_comap, comap_bot, ker_mkQ]
  -- ğŸ‰ no goals
#align submodule.mkq_map_self Submodule.mkQ_map_self

@[simp]
theorem comap_map_mkQ : comap p.mkQ (map p.mkQ p') = p âŠ” p' := by simp [comap_map_eq, sup_comm]
                                                                  -- ğŸ‰ no goals
#align submodule.comap_map_mkq Submodule.comap_map_mkQ

@[simp]
theorem map_mkQ_eq_top : map p.mkQ p' = âŠ¤ â†” p âŠ” p' = âŠ¤ := by
  -- porting note: ambiguity of `map_eq_top_iff` is no longer automatically resolved by preferring
  -- the current namespace
  simp only [LinearMap.map_eq_top_iff p.range_mkQ, sup_comm, ker_mkQ]
  -- ğŸ‰ no goals
#align submodule.map_mkq_eq_top Submodule.map_mkQ_eq_top

variable (q : Submodule Râ‚‚ Mâ‚‚)

/-- The map from the quotient of `M` by submodule `p` to the quotient of `Mâ‚‚` by submodule `q` along
`f : M â†’ Mâ‚‚` is linear. -/
def mapQ (f : M â†’â‚›â‚—[Ï„â‚â‚‚] Mâ‚‚) (h : p â‰¤ comap f q) : M â§¸ p â†’â‚›â‚—[Ï„â‚â‚‚] Mâ‚‚ â§¸ q :=
  p.liftQ (q.mkQ.comp f) <| by simpa [ker_comp] using h
                               -- ğŸ‰ no goals
#align submodule.mapq Submodule.mapQ

@[simp]
theorem mapQ_apply (f : M â†’â‚›â‚—[Ï„â‚â‚‚] Mâ‚‚) {h} (x : M) :
    mapQ p q f h (Quotient.mk x : M â§¸ p) = (Quotient.mk (f x) : Mâ‚‚ â§¸ q) :=
  rfl
#align submodule.mapq_apply Submodule.mapQ_apply

theorem mapQ_mkQ (f : M â†’â‚›â‚—[Ï„â‚â‚‚] Mâ‚‚) {h} : (mapQ p q f h).comp p.mkQ = q.mkQ.comp f := by
  ext x; rfl
  -- âŠ¢ â†‘(comp (mapQ p q f h) (mkQ p)) x = â†‘(comp (mkQ q) f) x
         -- ğŸ‰ no goals
#align submodule.mapq_mkq Submodule.mapQ_mkQ

@[simp]
theorem mapQ_zero (h : p â‰¤ q.comap (0 : M â†’â‚›â‚—[Ï„â‚â‚‚] Mâ‚‚) := (by simp)) :
                                                              -- ğŸ‰ no goals
    p.mapQ q (0 : M â†’â‚›â‚—[Ï„â‚â‚‚] Mâ‚‚) h = 0 := by
  ext
  -- âŠ¢ â†‘(comp (mapQ p q 0 h) (mkQ p)) xâœ = â†‘(comp 0 (mkQ p)) xâœ
  simp
  -- ğŸ‰ no goals
#align submodule.mapq_zero Submodule.mapQ_zero

/-- Given submodules `p âŠ† M`, `pâ‚‚ âŠ† Mâ‚‚`, `pâ‚ƒ âŠ† Mâ‚ƒ` and maps `f : M â†’ Mâ‚‚`, `g : Mâ‚‚ â†’ Mâ‚ƒ` inducing
`mapQ f : M â§¸ p â†’ Mâ‚‚ â§¸ pâ‚‚` and `mapQ g : Mâ‚‚ â§¸ pâ‚‚ â†’ Mâ‚ƒ â§¸ pâ‚ƒ` then
`mapQ (g âˆ˜ f) = (mapQ g) âˆ˜ (mapQ f)`. -/
theorem mapQ_comp {Râ‚ƒ Mâ‚ƒ : Type*} [Ring Râ‚ƒ] [AddCommGroup Mâ‚ƒ] [Module Râ‚ƒ Mâ‚ƒ] (pâ‚‚ : Submodule Râ‚‚ Mâ‚‚)
    (pâ‚ƒ : Submodule Râ‚ƒ Mâ‚ƒ) {Ï„â‚‚â‚ƒ : Râ‚‚ â†’+* Râ‚ƒ} {Ï„â‚â‚ƒ : R â†’+* Râ‚ƒ} [RingHomCompTriple Ï„â‚â‚‚ Ï„â‚‚â‚ƒ Ï„â‚â‚ƒ]
    (f : M â†’â‚›â‚—[Ï„â‚â‚‚] Mâ‚‚) (g : Mâ‚‚ â†’â‚›â‚—[Ï„â‚‚â‚ƒ] Mâ‚ƒ) (hf : p â‰¤ pâ‚‚.comap f) (hg : pâ‚‚ â‰¤ pâ‚ƒ.comap g)
    (h := hf.trans (comap_mono hg)) :
    p.mapQ pâ‚ƒ (g.comp f) h = (pâ‚‚.mapQ pâ‚ƒ g hg).comp (p.mapQ pâ‚‚ f hf) := by
  ext
  -- âŠ¢ â†‘(comp (mapQ p pâ‚ƒ (comp g f) h) (mkQ p)) xâœ = â†‘(comp (comp (mapQ pâ‚‚ pâ‚ƒ g hg) â€¦
  simp
  -- ğŸ‰ no goals
#align submodule.mapq_comp Submodule.mapQ_comp

@[simp]
theorem mapQ_id (h : p â‰¤ p.comap LinearMap.id := (by rw [comap_id])) :
                                                     -- ğŸ‰ no goals
    p.mapQ p LinearMap.id h = LinearMap.id := by
  ext
  -- âŠ¢ â†‘(comp (mapQ p p LinearMap.id h) (mkQ p)) xâœ = â†‘(comp LinearMap.id (mkQ p)) xâœ
  simp
  -- ğŸ‰ no goals
#align submodule.mapq_id Submodule.mapQ_id

theorem mapQ_pow {f : M â†’â‚—[R] M} (h : p â‰¤ p.comap f) (k : â„•)
    (h' : p â‰¤ p.comap (f ^ k) := p.le_comap_pow_of_le_comap h k) :
    p.mapQ p (f ^ k) h' = p.mapQ p f h ^ k := by
  induction' k with k ih
  -- âŠ¢ mapQ p p (f ^ Nat.zero) h' = mapQ p p f h ^ Nat.zero
  Â· simp [LinearMap.one_eq_id]
    -- ğŸ‰ no goals
  Â· simp only [LinearMap.iterate_succ]
    -- âŠ¢ mapQ p p (comp (f ^ k) f) (_ : p â‰¤ comap (comp (f ^ k) f) p) = comp (mapQ p  â€¦
    -- porting note: why does any of these `optParams` need to be applied? Why didn't `simp` handle
    -- all of this for us?
    convert mapQ_comp p p p f (f ^ k) h (p.le_comap_pow_of_le_comap h k)
      (h.trans (comap_mono <| p.le_comap_pow_of_le_comap h k))
    exact (ih _).symm
    -- ğŸ‰ no goals
#align submodule.mapq_pow Submodule.mapQ_pow

theorem comap_liftQ (f : M â†’â‚›â‚—[Ï„â‚â‚‚] Mâ‚‚) (h) : q.comap (p.liftQ f h) = (q.comap f).map (mkQ p) :=
  le_antisymm (by rintro âŸ¨xâŸ© hx; exact âŸ¨_, hx, rflâŸ©)
                  -- âŠ¢ Quot.mk Setoid.r x âˆˆ map (mkQ p) (comap f q)
                                 -- ğŸ‰ no goals
    (by rw [map_le_iff_le_comap, â† comap_comp, liftQ_mkQ])
        -- ğŸ‰ no goals
#align submodule.comap_liftq Submodule.comap_liftQ

theorem map_liftQ [RingHomSurjective Ï„â‚â‚‚] (f : M â†’â‚›â‚—[Ï„â‚â‚‚] Mâ‚‚) (h) (q : Submodule R (M â§¸ p)) :
    q.map (p.liftQ f h) = (q.comap p.mkQ).map f :=
  le_antisymm (by rintro _ âŸ¨âŸ¨xâŸ©, hxq, rflâŸ©; exact âŸ¨x, hxq, rflâŸ©)
                  -- âŠ¢ â†‘(liftQ p f h) (Quot.mk Setoid.r x) âˆˆ map f (comap (mkQ p) q)
                                            -- ğŸ‰ no goals
    (by rintro _ âŸ¨x, hxq, rflâŸ©; exact âŸ¨Quotient.mk x, hxq, rflâŸ©)
        -- âŠ¢ â†‘f x âˆˆ map (liftQ p f h) q
                                -- ğŸ‰ no goals
#align submodule.map_liftq Submodule.map_liftQ

theorem ker_liftQ (f : M â†’â‚›â‚—[Ï„â‚â‚‚] Mâ‚‚) (h) : ker (p.liftQ f h) = (ker f).map (mkQ p) :=
  comap_liftQ _ _ _ _
#align submodule.ker_liftq Submodule.ker_liftQ

theorem range_liftQ [RingHomSurjective Ï„â‚â‚‚] (f : M â†’â‚›â‚—[Ï„â‚â‚‚] Mâ‚‚) (h) :
    range (p.liftQ f h) = range f := by simpa only [range_eq_map] using map_liftQ _ _ _ _
                                        -- ğŸ‰ no goals
#align submodule.range_liftq Submodule.range_liftQ

theorem ker_liftQ_eq_bot (f : M â†’â‚›â‚—[Ï„â‚â‚‚] Mâ‚‚) (h) (h' : ker f â‰¤ p) : ker (p.liftQ f h) = âŠ¥ := by
  rw [ker_liftQ, le_antisymm h h', mkQ_map_self]
  -- ğŸ‰ no goals
#align submodule.ker_liftq_eq_bot Submodule.ker_liftQ_eq_bot

/-- The correspondence theorem for modules: there is an order isomorphism between submodules of the
quotient of `M` by `p`, and submodules of `M` larger than `p`. -/
def comapMkQRelIso : Submodule R (M â§¸ p) â‰ƒo { p' : Submodule R M // p â‰¤ p' } where
  toFun p' := âŸ¨comap p.mkQ p', le_comap_mkQ p _âŸ©
  invFun q := map p.mkQ q
  left_inv p' := map_comap_eq_self <| by simp
                                         -- ğŸ‰ no goals
  right_inv := fun âŸ¨q, hqâŸ© => Subtype.ext_val <| by simpa [comap_map_mkQ p]
                                                    -- ğŸ‰ no goals
  map_rel_iff' := comap_le_comap_iff <| range_mkQ _
#align submodule.comap_mkq.rel_iso Submodule.comapMkQRelIso

/-- The ordering on submodules of the quotient of `M` by `p` embeds into the ordering on submodules
of `M`. -/
def comapMkQOrderEmbedding : Submodule R (M â§¸ p) â†ªo Submodule R M :=
  (RelIso.toRelEmbedding <| comapMkQRelIso p).trans (Subtype.relEmbedding (Â· â‰¤ Â·) _)
#align submodule.comap_mkq.order_embedding Submodule.comapMkQOrderEmbedding

@[simp]
theorem comapMkQOrderEmbedding_eq (p' : Submodule R (M â§¸ p)) :
    comapMkQOrderEmbedding p p' = comap p.mkQ p' :=
  rfl
#align submodule.comap_mkq_embedding_eq Submodule.comapMkQOrderEmbedding_eq

theorem span_preimage_eq [RingHomSurjective Ï„â‚â‚‚] {f : M â†’â‚›â‚—[Ï„â‚â‚‚] Mâ‚‚} {s : Set Mâ‚‚} (hâ‚€ : s.Nonempty)
    (hâ‚ : s âŠ† range f) : span R (f â»Â¹' s) = (span Râ‚‚ s).comap f := by
  suffices (span Râ‚‚ s).comap f â‰¤ span R (f â»Â¹' s) by exact le_antisymm (span_preimage_le f s) this
  -- âŠ¢ comap f (span Râ‚‚ s) â‰¤ span R (â†‘f â»Â¹' s)
  have hk : ker f â‰¤ span R (f â»Â¹' s) := by
    let y := Classical.choose hâ‚€
    have hy : y âˆˆ s := Classical.choose_spec hâ‚€
    rw [ker_le_iff]
    use y, hâ‚ hy
    rw [â† Set.singleton_subset_iff] at hy
    exact Set.Subset.trans subset_span (span_mono (Set.preimage_mono hy))
  rw [â† left_eq_sup] at hk
  -- âŠ¢ comap f (span Râ‚‚ s) â‰¤ span R (â†‘f â»Â¹' s)
  rw [range_coe f] at hâ‚
  -- âŠ¢ comap f (span Râ‚‚ s) â‰¤ span R (â†‘f â»Â¹' s)
  rw [hk, â† LinearMap.map_le_map_iff, map_span, map_comap_eq, Set.image_preimage_eq_of_subset hâ‚]
  -- âŠ¢ range f âŠ“ span Râ‚‚ s â‰¤ span Râ‚‚ s
  exact inf_le_right
  -- ğŸ‰ no goals
#align submodule.span_preimage_eq Submodule.span_preimage_eq

/-- If `P` is a submodule of `M` and `Q` a submodule of `N`,
and `f : M â‰ƒâ‚— N` maps `P` to `Q`, then `M â§¸ P` is equivalent to `N â§¸ Q`. -/
@[simps]
def Quotient.equiv {N : Type*} [AddCommGroup N] [Module R N] (P : Submodule R M)
    (Q : Submodule R N) (f : M â‰ƒâ‚—[R] N) (hf : P.map f = Q) : (M â§¸ P) â‰ƒâ‚—[R] N â§¸ Q :=
  { P.mapQ Q (f : M â†’â‚—[R] N) fun x hx => hf â–¸ Submodule.mem_map_of_mem hx with
    toFun := P.mapQ Q (f : M â†’â‚—[R] N) fun x hx => hf â–¸ Submodule.mem_map_of_mem hx
    invFun :=
      Q.mapQ P (f.symm : N â†’â‚—[R] M) fun x hx => by
        rw [â† hf, Submodule.mem_map] at hx
        -- âŠ¢ x âˆˆ comap (â†‘(LinearEquiv.symm f)) P
        obtain âŸ¨y, hy, rflâŸ© := hx
        -- âŠ¢ â†‘f y âˆˆ comap (â†‘(LinearEquiv.symm f)) P
        simpa
        -- ğŸ‰ no goals
    left_inv := fun x => Quotient.inductionOn' x (by simp)
                                                     -- ğŸ‰ no goals
    right_inv := fun x => Quotient.inductionOn' x (by simp) }
                                                      -- ğŸ‰ no goals
#align submodule.quotient.equiv Submodule.Quotient.equiv
#align submodule.quotient.equiv_apply Submodule.Quotient.equiv_apply

@[simp]
theorem Quotient.equiv_symm {R M N : Type*} [CommRing R] [AddCommGroup M] [Module R M]
    [AddCommGroup N] [Module R N] (P : Submodule R M) (Q : Submodule R N) (f : M â‰ƒâ‚—[R] N)
    (hf : P.map f = Q) :
    (Quotient.equiv P Q f hf).symm =
      Quotient.equiv Q P f.symm ((Submodule.map_symm_eq_iff f).mpr hf) :=
  rfl
#align submodule.quotient.equiv_symm Submodule.Quotient.equiv_symm

@[simp]
theorem Quotient.equiv_trans {N O : Type*} [AddCommGroup N] [Module R N] [AddCommGroup O]
    [Module R O] (P : Submodule R M) (Q : Submodule R N) (S : Submodule R O) (e : M â‰ƒâ‚—[R] N)
    (f : N â‰ƒâ‚—[R] O) (he : P.map e = Q) (hf : Q.map f = S) (hef : P.map (e.trans f) = S) :
    Quotient.equiv P S (e.trans f) hef =
      (Quotient.equiv P Q e he).trans (Quotient.equiv Q S f hf) := by
  ext
  -- âŠ¢ â†‘(equiv P S (LinearEquiv.trans e f) hef) xâœ = â†‘(LinearEquiv.trans (equiv P Q â€¦
  -- `simp` can deal with `hef` depending on `e` and `f`
  simp only [Quotient.equiv_apply, LinearEquiv.trans_apply, LinearEquiv.coe_trans]
  -- âŠ¢ â†‘(mapQ P S (comp â†‘f â†‘e) (_ : P â‰¤ comap (comp â†‘f â†‘e) S)) xâœ = â†‘(mapQ Q S â†‘f ( â€¦
  -- `rw` can deal with `mapQ_comp` needing extra hypotheses coming from the RHS
  rw [mapQ_comp, LinearMap.comp_apply]
  -- ğŸ‰ no goals
#align submodule.quotient.equiv_trans Submodule.Quotient.equiv_trans

end Submodule

open Submodule

namespace LinearMap

section Ring

variable {R M Râ‚‚ Mâ‚‚ Râ‚ƒ Mâ‚ƒ : Type*}

variable [Ring R] [Ring Râ‚‚] [Ring Râ‚ƒ]

variable [AddCommMonoid M] [AddCommGroup Mâ‚‚] [AddCommMonoid Mâ‚ƒ]

variable [Module R M] [Module Râ‚‚ Mâ‚‚] [Module Râ‚ƒ Mâ‚ƒ]

variable {Ï„â‚â‚‚ : R â†’+* Râ‚‚} {Ï„â‚‚â‚ƒ : Râ‚‚ â†’+* Râ‚ƒ} {Ï„â‚â‚ƒ : R â†’+* Râ‚ƒ}

variable [RingHomCompTriple Ï„â‚â‚‚ Ï„â‚‚â‚ƒ Ï„â‚â‚ƒ] [RingHomSurjective Ï„â‚â‚‚]

theorem range_mkQ_comp (f : M â†’â‚›â‚—[Ï„â‚â‚‚] Mâ‚‚) : f.range.mkQ.comp f = 0 :=
  LinearMap.ext fun x => by simp
                            -- ğŸ‰ no goals
#align linear_map.range_mkq_comp LinearMap.range_mkQ_comp

theorem ker_le_range_iff {f : M â†’â‚›â‚—[Ï„â‚â‚‚] Mâ‚‚} {g : Mâ‚‚ â†’â‚›â‚—[Ï„â‚‚â‚ƒ] Mâ‚ƒ} :
    ker g â‰¤ range f â†” f.range.mkQ.comp g.ker.subtype = 0 := by
  rw [â† range_le_ker_iff, Submodule.ker_mkQ, Submodule.range_subtype]
  -- ğŸ‰ no goals
#align linear_map.ker_le_range_iff LinearMap.ker_le_range_iff

/-- An epimorphism is surjective. -/
theorem range_eq_top_of_cancel {f : M â†’â‚›â‚—[Ï„â‚â‚‚] Mâ‚‚}
    (h : âˆ€ u v : Mâ‚‚ â†’â‚—[Râ‚‚] Mâ‚‚ â§¸ (range f), u.comp f = v.comp f â†’ u = v) : range f = âŠ¤ := by
  have hâ‚ : (0 : Mâ‚‚ â†’â‚—[Râ‚‚] Mâ‚‚ â§¸ (range f)).comp f = 0 := zero_comp _
  -- âŠ¢ range f = âŠ¤
  rw [â† Submodule.ker_mkQ (range f), â† h 0 f.range.mkQ (Eq.trans hâ‚ (range_mkQ_comp _).symm)]
  -- âŠ¢ ker 0 = âŠ¤
  exact ker_zero
  -- ğŸ‰ no goals
#align linear_map.range_eq_top_of_cancel LinearMap.range_eq_top_of_cancel

end Ring

end LinearMap

open LinearMap

namespace Submodule

variable {R M : Type*} {r : R} {x y : M} [Ring R] [AddCommGroup M] [Module R M]

variable (p p' : Submodule R M)

/-- If `p = âŠ¥`, then `M / p â‰ƒâ‚—[R] M`. -/
def quotEquivOfEqBot (hp : p = âŠ¥) : (M â§¸ p) â‰ƒâ‚—[R] M :=
  LinearEquiv.ofLinear (p.liftQ id <| hp.symm â–¸ bot_le) p.mkQ (liftQ_mkQ _ _ _) <|
    p.quot_hom_ext _ LinearMap.id fun _ => rfl
#align submodule.quot_equiv_of_eq_bot Submodule.quotEquivOfEqBot

@[simp]
theorem quotEquivOfEqBot_apply_mk (hp : p = âŠ¥) (x : M) :
    p.quotEquivOfEqBot hp (Quotient.mk x : M â§¸ p) = x :=
  rfl
#align submodule.quot_equiv_of_eq_bot_apply_mk Submodule.quotEquivOfEqBot_apply_mk

@[simp]
theorem quotEquivOfEqBot_symm_apply (hp : p = âŠ¥) (x : M) :
    (p.quotEquivOfEqBot hp).symm x = (Quotient.mk x : M â§¸ p) :=
  rfl
#align submodule.quot_equiv_of_eq_bot_symm_apply Submodule.quotEquivOfEqBot_symm_apply

@[simp]
theorem coe_quotEquivOfEqBot_symm (hp : p = âŠ¥) :
    ((p.quotEquivOfEqBot hp).symm : M â†’â‚—[R] M â§¸ p) = p.mkQ :=
  rfl
#align submodule.coe_quot_equiv_of_eq_bot_symm Submodule.coe_quotEquivOfEqBot_symm

/-- Quotienting by equal submodules gives linearly equivalent quotients. -/
def quotEquivOfEq (h : p = p') : (M â§¸ p) â‰ƒâ‚—[R] M â§¸ p' :=
  { @Quotient.congr _ _ (quotientRel p) (quotientRel p') (Equiv.refl _) fun a b => by
      subst h
      -- âŠ¢ Setoid.r a b â†” Setoid.r (â†‘(Equiv.refl M) a) (â†‘(Equiv.refl M) b)
      rfl with
      -- ğŸ‰ no goals
    map_add' := by
      rintro âŸ¨xâŸ© âŸ¨yâŸ©
      -- âŠ¢ Equiv.toFun srcâœ (Quot.mk Setoid.r x + Quot.mk Setoid.r y) = Equiv.toFun src â€¦
      rfl
      -- ğŸ‰ no goals
    map_smul' := by
      rintro x âŸ¨yâŸ©
      -- âŠ¢ AddHom.toFun { toFun := srcâœ.toFun, map_add' := (_ : âˆ€ (x y : M â§¸ p), Equiv. â€¦
      rfl }
      -- ğŸ‰ no goals
#align submodule.quot_equiv_of_eq Submodule.quotEquivOfEq

@[simp]
theorem quotEquivOfEq_mk (h : p = p') (x : M) :
    Submodule.quotEquivOfEq p p' h (Submodule.Quotient.mk x : M â§¸ p) =
      (Submodule.Quotient.mk x : M â§¸ p') :=
  rfl
#align submodule.quot_equiv_of_eq_mk Submodule.quotEquivOfEq_mk

@[simp]
theorem Quotient.equiv_refl (P : Submodule R M) (Q : Submodule R M)
    (hf : P.map (LinearEquiv.refl R M : M â†’â‚—[R] M) = Q) :
    Quotient.equiv P Q (LinearEquiv.refl R M) hf = quotEquivOfEq _ _ (by simpa using hf) :=
                                                                         -- ğŸ‰ no goals
  rfl
#align submodule.quotient.equiv_refl Submodule.Quotient.equiv_refl

end Submodule

end Ring

section CommRing

variable {R M Mâ‚‚ : Type*} {r : R} {x y : M} [CommRing R] [AddCommGroup M] [Module R M]
  [AddCommGroup Mâ‚‚] [Module R Mâ‚‚] (p : Submodule R M) (q : Submodule R Mâ‚‚)

namespace Submodule

/-- Given modules `M`, `Mâ‚‚` over a commutative ring, together with submodules `p âŠ† M`, `q âŠ† Mâ‚‚`,
the natural map $\{f âˆˆ Hom(M, Mâ‚‚) | f(p) âŠ† q \} \to Hom(M/p, Mâ‚‚/q)$ is linear. -/
def mapQLinear : compatibleMaps p q â†’â‚—[R] M â§¸ p â†’â‚—[R] Mâ‚‚ â§¸ q
    where
  toFun f := mapQ _ _ f.val f.property
  map_add' x y := by
    ext
    -- âŠ¢ â†‘(LinearMap.comp ((fun f => mapQ p q â†‘f (_ : â†‘f âˆˆ compatibleMaps p q)) (x +  â€¦
    rfl
    -- ğŸ‰ no goals
  map_smul' c f := by
    ext
    -- âŠ¢ â†‘(LinearMap.comp (AddHom.toFun { toFun := fun f => mapQ p q â†‘f (_ : â†‘f âˆˆ com â€¦
    rfl
    -- ğŸ‰ no goals
#align submodule.mapq_linear Submodule.mapQLinear

end Submodule

end CommRing
