/-
Copyright (c) 2022 Michael Blyth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Blyth
-/
import Mathlib.LinearAlgebra.ProjectiveSpace.Basic

#align_import linear_algebra.projective_space.independence from "leanprover-community/mathlib"@"1e82f5ec4645f6a92bb9e02fce51e44e3bc3e1fe"

/-!
# Independence in Projective Space

In this file we define independence and dependence of families of elements in projective space.

## Implementation Details

We use an inductive definition to define the independence of points in projective
space, where the only constructor assumes an independent family of vectors from the
ambient vector space. Similarly for the definition of dependence.

## Results

- A family of elements is dependent if and only if it is not independent.
- Two elements are dependent if and only if they are equal.

# Future Work

- Define collinearity in projective space.
- Prove the axioms of a projective geometry are satisfied by the dependence relation.
- Define projective linear subspaces.
-/


variable {Î¹ K V : Type*} [Field K] [AddCommGroup V] [Module K V] {f : Î¹ â†’ â„™ K V}

namespace Projectivization

/-- A linearly independent family of nonzero vectors gives an independent family of points
in projective space. -/
inductive Independent : (Î¹ â†’ â„™ K V) â†’ Prop
  | mk (f : Î¹ â†’ V) (hf : âˆ€ i : Î¹, f i â‰  0) (hl : LinearIndependent K f) :
    Independent fun i => mk K (f i) (hf i)
#align projectivization.independent Projectivization.Independent

/-- A family of points in a projective space is independent if and only if the representative
vectors determined by the family are linearly independent. -/
theorem independent_iff : Independent f â†” LinearIndependent K (Projectivization.rep âˆ˜ f) := by
  refine' âŸ¨_, fun h => _âŸ©
  -- âŠ¢ Independent f â†’ LinearIndependent K (Projectivization.rep âˆ˜ f)
  Â· rintro âŸ¨ff, hff, hhâŸ©
    -- âŠ¢ LinearIndependent K (Projectivization.rep âˆ˜ fun i => mk K (ff i) (_ : ff i â‰  â€¦
    choose a ha using fun i : Î¹ => exists_smul_eq_mk_rep K (ff i) (hff i)
    -- âŠ¢ LinearIndependent K (Projectivization.rep âˆ˜ fun i => mk K (ff i) (_ : ff i â‰  â€¦
    convert hh.units_smul a
    -- âŠ¢ (Projectivization.rep âˆ˜ fun i => mk K (ff i) (_ : ff i â‰  0)) = a â€¢ ff
    ext i
    -- âŠ¢ (Projectivization.rep âˆ˜ fun i => mk K (ff i) (_ : ff i â‰  0)) i = (a â€¢ ff) i
    exact (ha i).symm
    -- ğŸ‰ no goals
  Â· convert Independent.mk _ _ h
    -- âŠ¢ f xâœ = mk K ((Projectivization.rep âˆ˜ f) xâœ) (_ : ?m.12147 xâœ â‰  0)
    Â· simp only [mk_rep, Function.comp_apply]
      -- ğŸ‰ no goals
    Â· intro i
      -- âŠ¢ (Projectivization.rep âˆ˜ f) i â‰  0
      apply rep_nonzero
      -- ğŸ‰ no goals
#align projectivization.independent_iff Projectivization.independent_iff

/-- A family of points in projective space is independent if and only if the family of
submodules which the points determine is independent in the lattice-theoretic sense. -/
theorem independent_iff_completeLattice_independent :
    Independent f â†” CompleteLattice.Independent fun i => (f i).submodule := by
  refine' âŸ¨_, fun h => _âŸ©
  -- âŠ¢ Independent f â†’ CompleteLattice.Independent fun i => Projectivization.submod â€¦
  Â· rintro âŸ¨f, hf, hiâŸ©
    -- âŠ¢ CompleteLattice.Independent fun i => Projectivization.submodule ((fun i => m â€¦
    simp only [submodule_mk]
    -- âŠ¢ CompleteLattice.Independent fun i => Submodule.span K {f i}
    exact (CompleteLattice.independent_iff_linearIndependent_of_ne_zero (R := K) hf).mpr hi
    -- ğŸ‰ no goals
  Â· rw [independent_iff]
    -- âŠ¢ LinearIndependent K (Projectivization.rep âˆ˜ f)
    refine' h.linearIndependent (Projectivization.submodule âˆ˜ f) (fun i => _) fun i => _
    -- âŠ¢ (Projectivization.rep âˆ˜ f) i âˆˆ (Projectivization.submodule âˆ˜ f) i
    Â· simpa only [Function.comp_apply, submodule_eq] using Submodule.mem_span_singleton_self _
      -- ğŸ‰ no goals
    Â· exact rep_nonzero (f i)
      -- ğŸ‰ no goals
#align projectivization.independent_iff_complete_lattice_independent Projectivization.independent_iff_completeLattice_independent

/-- A linearly dependent family of nonzero vectors gives a dependent family of points
in projective space. -/
inductive Dependent : (Î¹ â†’ â„™ K V) â†’ Prop
  | mk (f : Î¹ â†’ V) (hf : âˆ€ i : Î¹, f i â‰  0) (h : Â¬LinearIndependent K f) :
    Dependent fun i => mk K (f i) (hf i)
#align projectivization.dependent Projectivization.Dependent

/-- A family of points in a projective space is dependent if and only if their
representatives are linearly dependent. -/
theorem dependent_iff : Dependent f â†” Â¬LinearIndependent K (Projectivization.rep âˆ˜ f) := by
  refine' âŸ¨_, fun h => _âŸ©
  -- âŠ¢ Dependent f â†’ Â¬LinearIndependent K (Projectivization.rep âˆ˜ f)
  Â· rintro âŸ¨ff, hff, hh1âŸ©
    -- âŠ¢ Â¬LinearIndependent K (Projectivization.rep âˆ˜ fun i => mk K (ff i) (_ : ff i  â€¦
    contrapose! hh1
    -- âŠ¢ LinearIndependent K ff
    choose a ha using fun i : Î¹ => exists_smul_eq_mk_rep K (ff i) (hff i)
    -- âŠ¢ LinearIndependent K ff
    convert hh1.units_smul aâ»Â¹
    -- âŠ¢ ff = aâ»Â¹ â€¢ Projectivization.rep âˆ˜ fun i => mk K (ff i) (_ : ff i â‰  0)
    ext i
    -- âŠ¢ ff i = (aâ»Â¹ â€¢ Projectivization.rep âˆ˜ fun i => mk K (ff i) (_ : ff i â‰  0)) i
    simp only [â† ha, inv_smul_smul, Pi.smul_apply', Pi.inv_apply, Function.comp_apply]
    -- ğŸ‰ no goals
  Â· convert Dependent.mk _ _ h
    -- âŠ¢ f xâœ = mk K ((Projectivization.rep âˆ˜ f) xâœ) (_ : ?m.30442 xâœ â‰  0)
    Â· simp only [mk_rep, Function.comp_apply]
      -- ğŸ‰ no goals
    Â· exact fun i => rep_nonzero (f i)
      -- ğŸ‰ no goals
#align projectivization.dependent_iff Projectivization.dependent_iff

/-- Dependence is the negation of independence. -/
theorem dependent_iff_not_independent : Dependent f â†” Â¬Independent f := by
  rw [dependent_iff, independent_iff]
  -- ğŸ‰ no goals
#align projectivization.dependent_iff_not_independent Projectivization.dependent_iff_not_independent

/-- Independence is the negation of dependence. -/
theorem independent_iff_not_dependent : Independent f â†” Â¬Dependent f := by
  rw [dependent_iff_not_independent, Classical.not_not]
  -- ğŸ‰ no goals
#align projectivization.independent_iff_not_dependent Projectivization.independent_iff_not_dependent

/-- Two points in a projective space are dependent if and only if they are equal. -/
@[simp]
theorem dependent_pair_iff_eq (u v : â„™ K V) : Dependent ![u, v] â†” u = v := by
  rw [dependent_iff_not_independent, independent_iff, linearIndependent_fin2,
    Function.comp_apply, Matrix.cons_val_one, Matrix.head_cons, Ne.def]
  simp only [Matrix.cons_val_zero, not_and, not_forall, Classical.not_not, Function.comp_apply,
    â† mk_eq_mk_iff' K _ _ (rep_nonzero u) (rep_nonzero v), mk_rep, imp_iff_right_iff]
  exact Or.inl (rep_nonzero v)
  -- ğŸ‰ no goals
#align projectivization.dependent_pair_iff_eq Projectivization.dependent_pair_iff_eq

/-- Two points in a projective space are independent if and only if the points are not equal. -/
@[simp]
theorem independent_pair_iff_neq (u v : â„™ K V) : Independent ![u, v] â†” u â‰  v := by
  rw [independent_iff_not_dependent, dependent_pair_iff_eq u v]
  -- ğŸ‰ no goals
#align projectivization.independent_pair_iff_neq Projectivization.independent_pair_iff_neq

end Projectivization
