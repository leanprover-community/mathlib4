/-
Copyright (c) 2022 Michael Blyth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Blyth
-/
import Mathlib.LinearAlgebra.ProjectiveSpace.Basic

#align_import linear_algebra.projective_space.subspace from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!
# Subspaces of Projective Space

In this file we define subspaces of a projective space, and show that the subspaces of a projective
space form a complete lattice under inclusion.

## Implementation Details

A subspace of a projective space ‚Ñô K V is defined to be a structure consisting of a subset of
‚Ñô K V such that if two nonzero vectors in V determine points in ‚Ñô K V which are in the subset, and
the sum of the two vectors is nonzero, then the point determined by the sum of the two vectors is
also in the subset.

## Results

- There is a Galois insertion between the subsets of points of a projective space
  and the subspaces of the projective space, which is given by taking the span of the set of points.
- The subspaces of a projective space form a complete lattice under inclusion.

# Future Work
- Show that there is a one-to-one order-preserving correspondence between subspaces of a
  projective space and the submodules of the underlying vector space.
-/


variable (K V : Type*) [Field K] [AddCommGroup V] [Module K V]

namespace Projectivization

/-- A subspace of a projective space is a structure consisting of a set of points such that:
If two nonzero vectors determine points which are in the set, and the sum of the two vectors is
nonzero, then the point determined by the sum is also in the set. -/
@[ext]
structure Subspace where
  /-- The set of points. -/
  carrier : Set (‚Ñô K V)
  /-- The addition rule. -/
  mem_add' (v w : V) (hv : v ‚â† 0) (hw : w ‚â† 0) (hvw : v + w ‚â† 0) :
    mk K v hv ‚àà carrier ‚Üí mk K w hw ‚àà carrier ‚Üí mk K (v + w) hvw ‚àà carrier
#align projectivization.subspace Projectivization.Subspace

namespace Subspace

variable {K V}

instance : SetLike (Subspace K V) (‚Ñô K V) where
  coe := carrier
  coe_injective' A B := by
    cases A
    -- ‚ä¢ { carrier := carrier‚úù, mem_add' := mem_add'‚úù }.carrier = B.carrier ‚Üí { carri ‚Ä¶
    cases B
    -- ‚ä¢ { carrier := carrier‚úù¬π, mem_add' := mem_add'‚úù¬π }.carrier = { carrier := carr ‚Ä¶
    simp
    -- üéâ no goals

@[simp]
theorem mem_carrier_iff (A : Subspace K V) (x : ‚Ñô K V) : x ‚àà A.carrier ‚Üî x ‚àà A :=
  Iff.refl _
#align projectivization.subspace.mem_carrier_iff Projectivization.Subspace.mem_carrier_iff

theorem mem_add (T : Subspace K V) (v w : V) (hv : v ‚â† 0) (hw : w ‚â† 0) (hvw : v + w ‚â† 0) :
    Projectivization.mk K v hv ‚àà T ‚Üí
      Projectivization.mk K w hw ‚àà T ‚Üí Projectivization.mk K (v + w) hvw ‚àà T :=
  T.mem_add' v w hv hw hvw
#align projectivization.subspace.mem_add Projectivization.Subspace.mem_add

/-- The span of a set of points in a projective space is defined inductively to be the set of points
which contains the original set, and contains all points determined by the (nonzero) sum of two
nonzero vectors, each of which determine points in the span. -/
inductive spanCarrier (S : Set (‚Ñô K V)) : Set (‚Ñô K V)
  | of (x : ‚Ñô K V) (hx : x ‚àà S) : spanCarrier S x
  | mem_add (v w : V) (hv : v ‚â† 0) (hw : w ‚â† 0) (hvw : v + w ‚â† 0) :
      spanCarrier S (Projectivization.mk K v hv) ‚Üí
      spanCarrier S (Projectivization.mk K w hw) ‚Üí spanCarrier S (Projectivization.mk K (v + w) hvw)
#align projectivization.subspace.span_carrier Projectivization.Subspace.spanCarrier

/-- The span of a set of points in projective space is a subspace. -/
def span (S : Set (‚Ñô K V)) : Subspace K V where
  carrier := spanCarrier S
  mem_add' v w hv hw hvw := spanCarrier.mem_add v w hv hw hvw
#align projectivization.subspace.span Projectivization.Subspace.span

/-- The span of a set of points contains the set of points. -/
theorem subset_span (S : Set (‚Ñô K V)) : S ‚äÜ span S := fun _x hx => spanCarrier.of _ hx
#align projectivization.subspace.subset_span Projectivization.Subspace.subset_span

/-- The span of a set of points is a Galois insertion between sets of points of a projective space
and subspaces of the projective space. -/
def gi : GaloisInsertion (span : Set (‚Ñô K V) ‚Üí Subspace K V) SetLike.coe where
  choice S _hS := span S
  gc A B :=
    ‚ü®fun h => le_trans (subset_span _) h, by
      intro h x hx
      -- ‚ä¢ x ‚àà B
      induction' hx with y hy
      -- ‚ä¢ y ‚àà B
      ¬∑ apply h
        -- ‚ä¢ y ‚àà A
        assumption
        -- üéâ no goals
      ¬∑ apply B.mem_add
        assumption'‚ü©
        -- üéâ no goals
  le_l_u S := subset_span _
  choice_eq _ _ := rfl
#align projectivization.subspace.gi Projectivization.Subspace.gi

/-- The span of a subspace is the subspace. -/
@[simp]
theorem span_coe (W : Subspace K V) : span ‚ÜëW = W :=
  GaloisInsertion.l_u_eq gi W
#align projectivization.subspace.span_coe Projectivization.Subspace.span_coe

/-- The infimum of two subspaces exists. -/
instance instInf : Inf (Subspace K V) :=
  ‚ü®fun A B =>
    ‚ü®A ‚äì B, fun _v _w hv hw _hvw h1 h2 =>
      ‚ü®A.mem_add _ _ hv hw _ h1.1 h2.1, B.mem_add _ _ hv hw _ h1.2 h2.2‚ü©‚ü©‚ü©
#align projectivization.subspace.has_inf Projectivization.Subspace.instInf

-- Porting note: delete the name of this instance since it causes problem since hasInf is already
-- defined above
/-- Infimums of arbitrary collections of subspaces exist. -/
instance instInfSet : InfSet (Subspace K V) :=
  ‚ü®fun A =>
    ‚ü®sInf (SetLike.coe '' A), fun v w hv hw hvw h1 h2 t => by
      rintro ‚ü®s, hs, rfl‚ü©
      -- ‚ä¢ Projectivization.mk K (v + w) hvw ‚àà ‚Üës
      exact s.mem_add v w hv hw _ (h1 s ‚ü®s, hs, rfl‚ü©) (h2 s ‚ü®s, hs, rfl‚ü©)‚ü©‚ü©
      -- üéâ no goals
#align projectivization.subspace.has_Inf Projectivization.Subspace.instInfSet

/-- The subspaces of a projective space form a complete lattice. -/
instance : CompleteLattice (Subspace K V) :=
  { (inferInstance : Inf (Subspace K V)),
    completeLatticeOfInf (Subspace K V)
      (by
        refine fun s => ‚ü®fun a ha x hx => hx _ ‚ü®a, ha, rfl‚ü©, fun a ha x hx E => ?_‚ü©
        -- ‚ä¢ E ‚àà SetLike.coe '' s ‚Üí x ‚àà E
        rintro ‚ü®E, hE, rfl‚ü©
        -- ‚ä¢ x ‚àà ‚ÜëE
        exact ha hE hx) with
        -- üéâ no goals
    inf_le_left := fun A B _ hx => (@inf_le_left _ _ A B) hx
    inf_le_right := fun A B _ hx => (@inf_le_right _ _ A B) hx
    le_inf := fun A B _ h1 h2 _ hx => (le_inf h1 h2) hx }

instance subspaceInhabited : Inhabited (Subspace K V) where default := ‚ä§
#align projectivization.subspace.subspace_inhabited Projectivization.Subspace.subspaceInhabited

/-- The span of the empty set is the bottom of the lattice of subspaces. -/
@[simp]
theorem span_empty : span (‚àÖ : Set (‚Ñô K V)) = ‚ä• := gi.gc.l_bot
#align projectivization.subspace.span_empty Projectivization.Subspace.span_empty

/-- The span of the entire projective space is the top of the lattice of subspaces. -/
@[simp]
theorem span_univ : span (Set.univ : Set (‚Ñô K V)) = ‚ä§ := by
  rw [eq_top_iff, SetLike.le_def]
  -- ‚ä¢ ‚àÄ ‚¶Éx : ‚Ñô K V‚¶Ñ, x ‚àà ‚ä§ ‚Üí x ‚àà span Set.univ
  intro x _hx
  -- ‚ä¢ x ‚àà span Set.univ
  exact subset_span _ (Set.mem_univ x)
  -- üéâ no goals
#align projectivization.subspace.span_univ Projectivization.Subspace.span_univ

/-- The span of a set of points is contained in a subspace if and only if the set of points is
contained in the subspace. -/
theorem span_le_subspace_iff {S : Set (‚Ñô K V)} {W : Subspace K V} : span S ‚â§ W ‚Üî S ‚äÜ W :=
  gi.gc S W
#align projectivization.subspace.span_le_subspace_iff Projectivization.Subspace.span_le_subspace_iff

/-- If a set of points is a subset of another set of points, then its span will be contained in the
span of that set. -/
@[mono]
theorem monotone_span : Monotone (span : Set (‚Ñô K V) ‚Üí Subspace K V) :=
  gi.gc.monotone_l
#align projectivization.subspace.monotone_span Projectivization.Subspace.monotone_span

theorem subset_span_trans {S T U : Set (‚Ñô K V)} (hST : S ‚äÜ span T) (hTU : T ‚äÜ span U) :
    S ‚äÜ span U :=
  gi.gc.le_u_l_trans hST hTU
#align projectivization.subspace.subset_span_trans Projectivization.Subspace.subset_span_trans

/-- The supremum of two subspaces is equal to the span of their union. -/
theorem span_union (S T : Set (‚Ñô K V)) : span (S ‚à™ T) = span S ‚äî span T :=
  (@gi K V _ _ _).gc.l_sup
#align projectivization.subspace.span_union Projectivization.Subspace.span_union

/-- The supremum of a collection of subspaces is equal to the span of the union of the
collection. -/
theorem span_iUnion {Œπ} (s : Œπ ‚Üí Set (‚Ñô K V)) : span (‚ãÉ i, s i) = ‚®Ü i, span (s i) :=
  (@gi K V _ _ _).gc.l_iSup
#align projectivization.subspace.span_Union Projectivization.Subspace.span_iUnion

/-- The supremum of a subspace and the span of a set of points is equal to the span of the union of
the subspace and the set of points. -/
theorem sup_span {S : Set (‚Ñô K V)} {W : Subspace K V} : W ‚äî span S = span (W ‚à™ S) := by
  rw [span_union, span_coe]
  -- üéâ no goals
#align projectivization.subspace.sup_span Projectivization.Subspace.sup_span

theorem span_sup {S : Set (‚Ñô K V)} {W : Subspace K V} : span S ‚äî W = span (S ‚à™ W) := by
  rw [span_union, span_coe]
  -- üéâ no goals
#align projectivization.subspace.span_sup Projectivization.Subspace.span_sup

/-- A point in a projective space is contained in the span of a set of points if and only if the
point is contained in all subspaces of the projective space which contain the set of points. -/
theorem mem_span {S : Set (‚Ñô K V)} (u : ‚Ñô K V) :
    u ‚àà span S ‚Üî ‚àÄ W : Subspace K V, S ‚äÜ W ‚Üí u ‚àà W := by
  simp_rw [‚Üê span_le_subspace_iff]
  -- ‚ä¢ u ‚àà span S ‚Üî ‚àÄ (W : Subspace K V), span S ‚â§ W ‚Üí u ‚àà W
  exact ‚ü®fun hu W hW => hW hu, fun W => W (span S) (le_refl _)‚ü©
  -- üéâ no goals
#align projectivization.subspace.mem_span Projectivization.Subspace.mem_span

/-- The span of a set of points in a projective space is equal to the infimum of the collection of
subspaces which contain the set. -/
theorem span_eq_sInf {S : Set (‚Ñô K V)} : span S = sInf { W : Subspace K V| S ‚äÜ W } := by
  ext x
  -- ‚ä¢ x ‚àà (span S).carrier ‚Üî x ‚àà (sInf {W | S ‚äÜ ‚ÜëW}).carrier
  simp_rw [mem_carrier_iff, mem_span x]
  -- ‚ä¢ (‚àÄ (W : Subspace K V), S ‚äÜ ‚ÜëW ‚Üí x ‚àà W) ‚Üî x ‚àà sInf {W | S ‚äÜ ‚ÜëW}
  refine ‚ü®fun hx => ?_, fun hx W hW => ?_‚ü©
  -- ‚ä¢ x ‚àà sInf {W | S ‚äÜ ‚ÜëW}
  ¬∑ rintro W ‚ü®T, hT, rfl‚ü©
    -- ‚ä¢ x ‚àà ‚ÜëT
    exact hx T hT
    -- üéâ no goals
  ¬∑ exact (@sInf_le _ _ { W : Subspace K V | S ‚äÜ ‚ÜëW } W hW) hx
    -- üéâ no goals
#align projectivization.subspace.span_eq_Inf Projectivization.Subspace.span_eq_sInf

/-- If a set of points in projective space is contained in a subspace, and that subspace is
contained in the span of the set of points, then the span of the set of points is equal to
the subspace. -/
theorem span_eq_of_le {S : Set (‚Ñô K V)} {W : Subspace K V} (hS : S ‚äÜ W) (hW : W ‚â§ span S) :
    span S = W :=
  le_antisymm (span_le_subspace_iff.mpr hS) hW
#align projectivization.subspace.span_eq_of_le Projectivization.Subspace.span_eq_of_le

/-- The spans of two sets of points in a projective space are equal if and only if each set of
points is contained in the span of the other set. -/
theorem span_eq_span_iff {S T : Set (‚Ñô K V)} : span S = span T ‚Üî S ‚äÜ span T ‚àß T ‚äÜ span S :=
  ‚ü®fun h => ‚ü®h ‚ñ∏ subset_span S, h.symm ‚ñ∏ subset_span T‚ü©, fun h =>
    le_antisymm (span_le_subspace_iff.2 h.1) (span_le_subspace_iff.2 h.2)‚ü©
#align projectivization.subspace.span_eq_span_iff Projectivization.Subspace.span_eq_span_iff

end Subspace

end Projectivization
