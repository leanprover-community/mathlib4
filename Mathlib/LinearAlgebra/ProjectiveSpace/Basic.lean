/-
Copyright (c) 2022 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/
import Mathlib.LinearAlgebra.FiniteDimensional

#align_import linear_algebra.projective_space.basic from "leanprover-community/mathlib"@"c4658a649d216f57e99621708b09dcb3dcccbd23"

/-!

# Projective Spaces

This file contains the definition of the projectivization of a vector space over a field,
as well as the bijection between said projectivization and the collection of all one
dimensional subspaces of the vector space.

## Notation
`‚Ñô K V` is notation for `Projectivization K V`, the projectivization of a `K`-vector space `V`.

## Constructing terms of `‚Ñô K V`.
We have three ways to construct terms of `‚Ñô K V`:
- `Projectivization.mk K v hv` where `v : V` and `hv : v ‚â† 0`.
- `Projectivization.mk' K v` where `v : { w : V // w ‚â† 0 }`.
- `Projectivization.mk'' H h` where `H : Submodule K V` and `h : finrank H = 1`.

## Other definitions
- For `v : ‚Ñô K V`, `v.submodule` gives the corresponding submodule of `V`.
- `Projectivization.equivSubmodule` is the equivalence between `‚Ñô K V`
  and `{ H : Submodule K V // finrank H = 1 }`.
- For `v : ‚Ñô K V`, `v.rep : V` is a representative of `v`.

-/


variable (K V : Type*) [DivisionRing K] [AddCommGroup V] [Module K V]

/-- The setoid whose quotient is the projectivization of `V`. -/
def projectivizationSetoid : Setoid { v : V // v ‚â† 0 } :=
  (MulAction.orbitRel KÀ£ V).comap (‚Üë)
#align projectivization_setoid projectivizationSetoid

/-- The projectivization of the `K`-vector space `V`.
The notation `‚Ñô K V` is preferred. -/
def Projectivization := Quotient (projectivizationSetoid K V)
#align projectivization Projectivization

notation "‚Ñô" => Projectivization

namespace Projectivization

variable {V}

/-- Construct an element of the projectivization from a nonzero vector. -/
def mk (v : V) (hv : v ‚â† 0) : ‚Ñô K V :=
  Quotient.mk'' ‚ü®v, hv‚ü©
#align projectivization.mk Projectivization.mk

/-- A variant of `Projectivization.mk` in terms of a subtype. `mk` is preferred. -/
def mk' (v : { v : V // v ‚â† 0 }) : ‚Ñô K V :=
  Quotient.mk'' v
#align projectivization.mk' Projectivization.mk'

@[simp]
theorem mk'_eq_mk (v : { v : V // v ‚â† 0 }) : mk' K v = mk K ‚Üëv v.2 := rfl
#align projectivization.mk'_eq_mk Projectivization.mk'_eq_mk

instance [Nontrivial V] : Nonempty (‚Ñô K V) :=
  let ‚ü®v, hv‚ü© := exists_ne (0 : V)
  ‚ü®mk K v hv‚ü©

variable {K}

/-- Choose a representative of `v : Projectivization K V` in `V`. -/
protected noncomputable def rep (v : ‚Ñô K V) : V :=
  v.out'
#align projectivization.rep Projectivization.rep

theorem rep_nonzero (v : ‚Ñô K V) : v.rep ‚â† 0 :=
  v.out'.2
#align projectivization.rep_nonzero Projectivization.rep_nonzero

@[simp]
theorem mk_rep (v : ‚Ñô K V) : mk K v.rep v.rep_nonzero = v := Quotient.out_eq' _
#align projectivization.mk_rep Projectivization.mk_rep

open FiniteDimensional

/-- Consider an element of the projectivization as a submodule of `V`. -/
protected def submodule (v : ‚Ñô K V) : Submodule K V :=
  (Quotient.liftOn' v fun v => K ‚àô (v : V)) <| by
    rintro ‚ü®a, ha‚ü© ‚ü®b, hb‚ü© ‚ü®x, rfl : x ‚Ä¢ b = a‚ü©
    -- ‚ä¢ Submodule.span K {‚Üë{ val := x ‚Ä¢ b, property := ha }} = Submodule.span K {‚Üë{  ‚Ä¶
    exact Submodule.span_singleton_group_smul_eq _ x _
    -- üéâ no goals
#align projectivization.submodule Projectivization.submodule

variable (K)

theorem mk_eq_mk_iff (v w : V) (hv : v ‚â† 0) (hw : w ‚â† 0) :
    mk K v hv = mk K w hw ‚Üî ‚àÉ a : KÀ£, a ‚Ä¢ w = v :=
  Quotient.eq''
#align projectivization.mk_eq_mk_iff Projectivization.mk_eq_mk_iff

/-- Two nonzero vectors go to the same point in projective space if and only if one is
a scalar multiple of the other. -/
theorem mk_eq_mk_iff' (v w : V) (hv : v ‚â† 0) (hw : w ‚â† 0) :
    mk K v hv = mk K w hw ‚Üî ‚àÉ a : K, a ‚Ä¢ w = v := by
  rw [mk_eq_mk_iff K v w hv hw]
  -- ‚ä¢ (‚àÉ a, a ‚Ä¢ w = v) ‚Üî ‚àÉ a, a ‚Ä¢ w = v
  constructor
  -- ‚ä¢ (‚àÉ a, a ‚Ä¢ w = v) ‚Üí ‚àÉ a, a ‚Ä¢ w = v
  ¬∑ rintro ‚ü®a, ha‚ü©
    -- ‚ä¢ ‚àÉ a, a ‚Ä¢ w = v
    exact ‚ü®a, ha‚ü©
    -- üéâ no goals
  ¬∑ rintro ‚ü®a, ha‚ü©
    -- ‚ä¢ ‚àÉ a, a ‚Ä¢ w = v
    refine' ‚ü®Units.mk0 a fun c => hv.symm _, ha‚ü©
    -- ‚ä¢ 0 = v
    rwa [c, zero_smul] at ha
    -- üéâ no goals
#align projectivization.mk_eq_mk_iff' Projectivization.mk_eq_mk_iff'

theorem exists_smul_eq_mk_rep (v : V) (hv : v ‚â† 0) : ‚àÉ a : KÀ£, a ‚Ä¢ v = (mk K v hv).rep :=
  (mk_eq_mk_iff K _ _ (rep_nonzero _) hv).1 (mk_rep _)
#align projectivization.exists_smul_eq_mk_rep Projectivization.exists_smul_eq_mk_rep

variable {K}

/-- An induction principle for `Projectivization`.
Use as `induction v using Projectivization.ind`. -/
@[elab_as_elim]
theorem ind {P : ‚Ñô K V ‚Üí Prop} (h : ‚àÄ (v : V) (h : v ‚â† 0), P (mk K v h)) : ‚àÄ p, P p :=
  Quotient.ind' <| Subtype.rec <| h
#align projectivization.ind Projectivization.ind

@[simp]
theorem submodule_mk (v : V) (hv : v ‚â† 0) : (mk K v hv).submodule = K ‚àô v :=
  rfl
#align projectivization.submodule_mk Projectivization.submodule_mk

theorem submodule_eq (v : ‚Ñô K V) : v.submodule = K ‚àô v.rep := by
  conv_lhs => rw [‚Üê v.mk_rep]
  -- üéâ no goals
#align projectivization.submodule_eq Projectivization.submodule_eq

theorem finrank_submodule (v : ‚Ñô K V) : finrank K v.submodule = 1 := by
  rw [submodule_eq]
  -- ‚ä¢ finrank K { x // x ‚àà Submodule.span K {Projectivization.rep v} } = 1
  exact finrank_span_singleton v.rep_nonzero
  -- üéâ no goals
#align projectivization.finrank_submodule Projectivization.finrank_submodule

instance (v : ‚Ñô K V) : FiniteDimensional K v.submodule := by
  rw [‚Üê v.mk_rep]
  -- ‚ä¢ FiniteDimensional K { x // x ‚àà Projectivization.submodule (mk K (Projectiviz ‚Ä¶
  change FiniteDimensional K (K ‚àô v.rep)
  -- ‚ä¢ FiniteDimensional K { x // x ‚àà Submodule.span K {Projectivization.rep v} }
  infer_instance
  -- üéâ no goals

theorem submodule_injective :
    Function.Injective (Projectivization.submodule : ‚Ñô K V ‚Üí Submodule K V) := fun u v h ‚Ü¶ by
  induction' u using ind with u hu
  -- ‚ä¢ mk K u hu = v
  induction' v using ind with v hv
  -- ‚ä¢ mk K u hu = mk K v hv
  rw [submodule_mk, submodule_mk, Submodule.span_singleton_eq_span_singleton] at h
  -- ‚ä¢ mk K u hu = mk K v hv
  exact ((mk_eq_mk_iff K v u hv hu).2 h).symm
  -- üéâ no goals
#align projectivization.submodule_injective Projectivization.submodule_injective

variable (K V)

/-- The equivalence between the projectivization and the
collection of subspaces of dimension 1. -/
noncomputable def equivSubmodule : ‚Ñô K V ‚âÉ { H : Submodule K V // finrank K H = 1 } :=
  (Equiv.ofInjective _ submodule_injective).trans <| .subtypeEquiv (.refl _) fun H ‚Ü¶ by
    refine ‚ü®fun ‚ü®v, hv‚ü© ‚Ü¶ hv ‚ñ∏ v.finrank_submodule, fun h ‚Ü¶ ?_‚ü©
    -- ‚ä¢ H ‚àà Set.range Projectivization.submodule
    rcases finrank_eq_one_iff'.1 h with ‚ü®v : H, hv‚ÇÄ, hv : ‚àÄ w : H, _‚ü©
    -- ‚ä¢ H ‚àà Set.range Projectivization.submodule
    use mk K (v : V) (Subtype.coe_injective.ne hv‚ÇÄ)
    -- ‚ä¢ Projectivization.submodule (mk K ‚Üëv (_ : ‚Üëv ‚â† ‚Üë0)) = H
    rw [submodule_mk, SetLike.ext'_iff, Submodule.span_singleton_eq_range]
    -- ‚ä¢ (Set.range fun x => x ‚Ä¢ ‚Üëv) = ‚ÜëH
    refine (Set.range_subset_iff.2 fun _ ‚Ü¶ H.smul_mem _ v.2).antisymm fun x hx ‚Ü¶ ?_
    -- ‚ä¢ x ‚àà Set.range fun x => x ‚Ä¢ ‚Üëv
    rcases hv ‚ü®x, hx‚ü© with ‚ü®c, hc‚ü©
    -- ‚ä¢ x ‚àà Set.range fun x => x ‚Ä¢ ‚Üëv
    exact ‚ü®c, congr_arg Subtype.val hc‚ü©
    -- üéâ no goals
#align projectivization.equiv_submodule Projectivization.equivSubmodule

variable {K V}

/-- Construct an element of the projectivization from a subspace of dimension 1. -/
noncomputable def mk'' (H : Submodule K V) (h : finrank K H = 1) : ‚Ñô K V :=
  (equivSubmodule K V).symm ‚ü®H, h‚ü©
#align projectivization.mk'' Projectivization.mk''

@[simp]
theorem submodule_mk'' (H : Submodule K V) (h : finrank K H = 1) : (mk'' H h).submodule = H :=
  congr_arg Subtype.val <| (equivSubmodule K V).apply_symm_apply ‚ü®H, h‚ü©
#align projectivization.submodule_mk'' Projectivization.submodule_mk''

@[simp]
theorem mk''_submodule (v : ‚Ñô K V) : mk'' v.submodule v.finrank_submodule = v :=
  (equivSubmodule K V).symm_apply_apply v
#align projectivization.mk''_submodule Projectivization.mk''_submodule

section Map

variable {L W : Type*} [DivisionRing L] [AddCommGroup W] [Module L W]

/-- An injective semilinear map of vector spaces induces a map on projective spaces. -/
def map {œÉ : K ‚Üí+* L} (f : V ‚Üí‚Çõ‚Çó[œÉ] W) (hf : Function.Injective f) : ‚Ñô K V ‚Üí ‚Ñô L W :=
  Quotient.map' (fun v => ‚ü®f v, fun c => v.2 (hf (by simp [c]))‚ü©)
                                                     -- üéâ no goals
    (by
      rintro ‚ü®u, hu‚ü© ‚ü®v, hv‚ü© ‚ü®a, ha‚ü©
      -- ‚ä¢ Setoid.r ((fun v => { val := ‚Üëf ‚Üëv, property := (_ : ‚Üëf ‚Üëv = 0 ‚Üí False) }) { ‚Ä¶
      use Units.map œÉ.toMonoidHom a
      -- ‚ä¢ (fun m => m ‚Ä¢ ‚Üë((fun v => { val := ‚Üëf ‚Üëv, property := (_ : ‚Üëf ‚Üëv = 0 ‚Üí False ‚Ä¶
      dsimp at ha ‚ä¢
      -- ‚ä¢ ‚Üë(Units.map ‚ÜëœÉ) a ‚Ä¢ ‚Üëf v = ‚Üëf u
      erw [‚Üê f.map_smul‚Çõ‚Çó, ha])
      -- üéâ no goals
#align projectivization.map Projectivization.map

theorem map_mk {œÉ : K ‚Üí+* L} (f : V ‚Üí‚Çõ‚Çó[œÉ] W) (hf : Function.Injective f) (v : V) (hv : v ‚â† 0) :
    map f hf (mk K v hv) = mk L (f v) (map_zero f ‚ñ∏ hf.ne hv) :=
  rfl

/-- Mapping with respect to a semilinear map over an isomorphism of fields yields
an injective map on projective spaces. -/
theorem map_injective {œÉ : K ‚Üí+* L} {œÑ : L ‚Üí+* K} [RingHomInvPair œÉ œÑ] (f : V ‚Üí‚Çõ‚Çó[œÉ] W)
    (hf : Function.Injective f) : Function.Injective (map f hf) := fun u v h ‚Ü¶ by
  induction' u using ind with u hu; induction' v using ind with v hv
  -- ‚ä¢ mk K u hu = v
                                    -- ‚ä¢ mk K u hu = mk K v hv
  simp only [map_mk, mk_eq_mk_iff'] at h ‚ä¢
  -- ‚ä¢ ‚àÉ a, a ‚Ä¢ v = u
  rcases h with ‚ü®a, ha‚ü©
  -- ‚ä¢ ‚àÉ a, a ‚Ä¢ v = u
  refine ‚ü®œÑ a, hf ?_‚ü©
  -- ‚ä¢ ‚Üëf (‚ÜëœÑ a ‚Ä¢ v) = ‚Üëf u
  rwa [f.map_smul‚Çõ‚Çó, RingHomInvPair.comp_apply_eq‚ÇÇ]
  -- üéâ no goals
#align projectivization.map_injective Projectivization.map_injective

@[simp]
theorem map_id : map (LinearMap.id : V ‚Üí‚Çó[K] V) (LinearEquiv.refl K V).injective = id := by
  ext ‚ü®v‚ü©
  -- ‚ä¢ map LinearMap.id (_ : Function.Injective ‚Üë(LinearEquiv.refl K V)) (Quot.mk S ‚Ä¶
  rfl
  -- üéâ no goals
#align projectivization.map_id Projectivization.map_id

-- porting note: removed `@[simp]` because of unusable `hg.comp hf` in the LHS
theorem map_comp {F U : Type*} [Field F] [AddCommGroup U] [Module F U] {œÉ : K ‚Üí+* L} {œÑ : L ‚Üí+* F}
    {Œ≥ : K ‚Üí+* F} [RingHomCompTriple œÉ œÑ Œ≥] (f : V ‚Üí‚Çõ‚Çó[œÉ] W) (hf : Function.Injective f)
    (g : W ‚Üí‚Çõ‚Çó[œÑ] U) (hg : Function.Injective g) :
    map (g.comp f) (hg.comp hf) = map g hg ‚àò map f hf := by
  ext ‚ü®v‚ü©
  -- ‚ä¢ map (LinearMap.comp g f) (_ : Function.Injective (‚Üëg ‚àò fun x => ‚Üëf x)) (Quot ‚Ä¶
  rfl
  -- üéâ no goals
#align projectivization.map_comp Projectivization.map_comp

end Map

end Projectivization
