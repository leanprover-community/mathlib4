/-
Copyright (c) 2022 Anne Baanen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anne Baanen, Alex J. Best
-/
import Mathlib.LinearAlgebra.Pi
import Mathlib.LinearAlgebra.Quotient

#align_import linear_algebra.quotient_pi from "leanprover-community/mathlib"@"398f60f60b43ef42154bd2bdadf5133daf1577a4"

/-!
# Submodule quotients and direct sums

This file contains some results on the quotient of a module by a direct sum of submodules,
and the direct sum of quotients of modules by submodules.

# Main definitions

 * `Submodule.piQuotientLift`: create a map out of the direct sum of quotients
 * `Submodule.quotientPiLift`: create a map out of the quotient of a direct sum
 * `Submodule.quotientPi`: the quotient of a direct sum is the direct sum of quotients.

-/


namespace Submodule

open LinearMap

variable {Î¹ R : Type*} [CommRing R]

variable {Ms : Î¹ â†’ Type*} [âˆ€ i, AddCommGroup (Ms i)] [âˆ€ i, Module R (Ms i)]

variable {N : Type*} [AddCommGroup N] [Module R N]

variable {Ns : Î¹ â†’ Type*} [âˆ€ i, AddCommGroup (Ns i)] [âˆ€ i, Module R (Ns i)]

/-- Lift a family of maps to the direct sum of quotients. -/
def piQuotientLift [Fintype Î¹] [DecidableEq Î¹] (p : âˆ€ i, Submodule R (Ms i)) (q : Submodule R N)
    (f : âˆ€ i, Ms i â†’â‚—[R] N) (hf : âˆ€ i, p i â‰¤ q.comap (f i)) : (âˆ€ i, Ms i â§¸ p i) â†’â‚—[R] N â§¸ q :=
  lsum R (fun i => Ms i â§¸ p i) R fun i => (p i).mapQ q (f i) (hf i)
#align submodule.pi_quotient_lift Submodule.piQuotientLift

@[simp]
theorem piQuotientLift_mk [Fintype Î¹] [DecidableEq Î¹] (p : âˆ€ i, Submodule R (Ms i))
    (q : Submodule R N) (f : âˆ€ i, Ms i â†’â‚—[R] N) (hf : âˆ€ i, p i â‰¤ q.comap (f i)) (x : âˆ€ i, Ms i) :
    (piQuotientLift p q f hf fun i => Quotient.mk (x i)) = Quotient.mk (lsum _ _ R f x) := by
  rw [piQuotientLift, lsum_apply, sum_apply, â† mkQ_apply, lsum_apply, sum_apply, _root_.map_sum]
  -- âŠ¢ (Finset.sum Finset.univ fun d => â†‘(comp (mapQ (p d) q (f d) (_ : p d â‰¤ comap â€¦
  simp only [coe_proj, mapQ_apply, mkQ_apply, comp_apply]
  -- ðŸŽ‰ no goals
#align submodule.pi_quotient_lift_mk Submodule.piQuotientLift_mk

@[simp]
theorem piQuotientLift_single [Fintype Î¹] [DecidableEq Î¹] (p : âˆ€ i, Submodule R (Ms i))
    (q : Submodule R N) (f : âˆ€ i, Ms i â†’â‚—[R] N) (hf : âˆ€ i, p i â‰¤ q.comap (f i)) (i)
    (x : Ms i â§¸ p i) : piQuotientLift p q f hf (Pi.single i x) = mapQ _ _ (f i) (hf i) x := by
  simp_rw [piQuotientLift, lsum_apply, sum_apply, comp_apply, proj_apply]
  -- âŠ¢ (Finset.sum Finset.univ fun x_1 => â†‘(mapQ (p x_1) q (f x_1) (_ : p x_1 â‰¤ com â€¦
  rw [Finset.sum_eq_single i]
  Â· rw [Pi.single_eq_same]
    -- ðŸŽ‰ no goals
  Â· rintro j - hj
    -- âŠ¢ â†‘(mapQ (p j) q (f j) (_ : p j â‰¤ comap (f j) q)) (Pi.single i x j) = 0
    rw [Pi.single_eq_of_ne hj, _root_.map_zero]
    -- ðŸŽ‰ no goals
  Â· intros
    -- âŠ¢ â†‘(mapQ (p i) q (f i) (_ : p i â‰¤ comap (f i) q)) (Pi.single i x i) = 0
    have := Finset.mem_univ i
    -- âŠ¢ â†‘(mapQ (p i) q (f i) (_ : p i â‰¤ comap (f i) q)) (Pi.single i x i) = 0
    contradiction
    -- ðŸŽ‰ no goals
#align submodule.pi_quotient_lift_single Submodule.piQuotientLift_single

/-- Lift a family of maps to a quotient of direct sums. -/
def quotientPiLift (p : âˆ€ i, Submodule R (Ms i)) (f : âˆ€ i, Ms i â†’â‚—[R] Ns i)
    (hf : âˆ€ i, p i â‰¤ ker (f i)) : (âˆ€ i, Ms i) â§¸ pi Set.univ p â†’â‚—[R] âˆ€ i, Ns i :=
  (pi Set.univ p).liftQ (LinearMap.pi fun i => (f i).comp (proj i)) fun x hx =>
    mem_ker.mpr <| by
      ext i
      -- âŠ¢ â†‘(LinearMap.pi fun i => comp (f i) (proj i)) x i = OfNat.ofNat 0 i
      simpa using hf i (mem_pi.mp hx i (Set.mem_univ i))
      -- ðŸŽ‰ no goals
#align submodule.quotient_pi_lift Submodule.quotientPiLift

@[simp]
theorem quotientPiLift_mk (p : âˆ€ i, Submodule R (Ms i)) (f : âˆ€ i, Ms i â†’â‚—[R] Ns i)
    (hf : âˆ€ i, p i â‰¤ ker (f i)) (x : âˆ€ i, Ms i) :
    quotientPiLift p f hf (Quotient.mk x) = fun i => f i (x i) :=
  rfl
#align submodule.quotient_pi_lift_mk Submodule.quotientPiLift_mk

-- Porting note: split up the definition to avoid timeouts. Still slow.
namespace quotientPi_aux

variable [Fintype Î¹] [DecidableEq Î¹] (p : âˆ€ i, Submodule R (Ms i))

@[simp]
def toFun : ((âˆ€ i, Ms i) â§¸ pi Set.univ p) â†’ âˆ€ i, Ms i â§¸ p i :=
  quotientPiLift p (fun i => (p i).mkQ) fun i => (ker_mkQ (p i)).ge

@[simp]
def invFun : (âˆ€ i, Ms i â§¸ p i) â†’ (âˆ€ i, Ms i) â§¸ pi Set.univ p :=
  piQuotientLift p (pi Set.univ p) single fun _ => le_comap_single_pi p

theorem left_inv : Function.LeftInverse (invFun p) (toFun p) := fun x =>
  Quotient.inductionOn' x fun x' => by
    rw [Quotient.mk''_eq_mk x']
    -- âŠ¢ invFun p (toFun p (Quotient.mk x')) = Quotient.mk x'
    dsimp only [toFun, invFun]
    -- âŠ¢ â†‘(piQuotientLift p (pi Set.univ p) single (_ : âˆ€ (x : Î¹), p x â‰¤ comap (singl â€¦
    rw [quotientPiLift_mk p, funext fun i => (mkQ_apply (p i) (x' i)), piQuotientLift_mk p,
      lsum_single, id_apply]

theorem right_inv : Function.RightInverse (invFun p) (toFun p) := by
  dsimp only [toFun, invFun]
  -- âŠ¢ Function.RightInverse â†‘(piQuotientLift p (pi Set.univ p) single (_ : âˆ€ (x :  â€¦
  rw [Function.rightInverse_iff_comp, â† coe_comp, â† @id_coe R]
  -- âŠ¢ â†‘(comp (quotientPiLift p (fun i => mkQ (p i)) (_ : âˆ€ (i : Î¹), p i â‰¤ ker (mkQ â€¦
  refine' congr_arg _ (pi_ext fun i x => Quotient.inductionOn' x fun x' => funext fun j => _)
  -- âŠ¢ â†‘(comp (quotientPiLift p (fun i => mkQ (p i)) (_ : âˆ€ (i : Î¹), p i â‰¤ ker (mkQ â€¦
  rw [comp_apply, piQuotientLift_single, Quotient.mk''_eq_mk, mapQ_apply,
    quotientPiLift_mk, id_apply]
  by_cases hij : i = j <;> simp only [mkQ_apply, coe_single]
  -- âŠ¢ (fun i_1 => â†‘(mkQ (p i_1)) (â†‘(single i) x' i_1)) j = Pi.single i (Quotient.m â€¦
                           -- âŠ¢ Quotient.mk (Pi.single i x' j) = Pi.single i (Quotient.mk x') j
                           -- âŠ¢ Quotient.mk (Pi.single i x' j) = Pi.single i (Quotient.mk x') j
  Â· subst hij
    -- âŠ¢ Quotient.mk (Pi.single i x' i) = Pi.single i (Quotient.mk x') i
    rw [Pi.single_eq_same, Pi.single_eq_same]
    -- ðŸŽ‰ no goals
  Â· rw [Pi.single_eq_of_ne (Ne.symm hij), Pi.single_eq_of_ne (Ne.symm hij), Quotient.mk_zero]
    -- ðŸŽ‰ no goals

theorem map_add (x y : ((i : Î¹) â†’ Ms i) â§¸ pi Set.univ p) :
    toFun p (x + y) = toFun p x + toFun p y :=
  LinearMap.map_add (quotientPiLift p (fun i => (p i).mkQ) fun i => (ker_mkQ (p i)).ge) x y

theorem map_smul (r : R) (x : ((i : Î¹) â†’ Ms i) â§¸ pi Set.univ p) :
    toFun p (r â€¢ x) = (RingHom.id R r) â€¢ toFun p x :=
  LinearMap.map_smul (quotientPiLift p (fun i => (p i).mkQ) fun i => (ker_mkQ (p i)).ge) r x

end quotientPi_aux

open quotientPi_aux in
/-- The quotient of a direct sum is the direct sum of quotients. -/
@[simps!]
def quotientPi [Fintype Î¹] [DecidableEq Î¹] (p : âˆ€ i, Submodule R (Ms i)) :
    ((âˆ€ i, Ms i) â§¸ pi Set.univ p) â‰ƒâ‚—[R] âˆ€ i, Ms i â§¸ p i where
  toFun := toFun p
  invFun := invFun p
  map_add' := map_add p
  map_smul' := quotientPi_aux.map_smul p
  left_inv := left_inv p
  right_inv := right_inv p
#align submodule.quotient_pi Submodule.quotientPi

end Submodule
