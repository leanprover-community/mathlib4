/-
Copyright (c) 2025 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import Mathlib.LinearAlgebra.RootSystem.Finite.Lemmas
import Mathlib.LinearAlgebra.RootSystem.Finite.G2
import Mathlib.Order.Interval.Set.OrdConnectedLinear

/-!
# Chains of roots

Given roots `Œ±` and `Œ≤`, the `Œ±`-chain through `Œ≤` is the set of roots of the form `Œ± + z ‚Ä¢ Œ≤`
for an integer `z`. This is known as a "root chain" and also a "root string". For linearly
independent roots in finite crystallographic root pairings, these chains are always unbroken, i.e.,
of the form: `Œ≤ - q ‚Ä¢ Œ±, ..., Œ≤ - Œ±, Œ≤, Œ≤ + Œ±, ..., Œ≤ + p ‚Ä¢ Œ±` for natural numbers `p`, `q`, and the
length, `p + q` is at most 3.

## Main definitions / results:
* `RootPairing.chainTopCoeff`: the natural number `p` in the chain
  `Œ≤ - q ‚Ä¢ Œ±, ..., Œ≤ - Œ±, Œ≤, Œ≤ + Œ±, ..., Œ≤ + p ‚Ä¢ Œ±`
* `RootPairing.chainTopCoeff`: the natural number `q` in the chain
  `Œ≤ - q ‚Ä¢ Œ±, ..., Œ≤ - Œ±, Œ≤, Œ≤ + Œ±, ..., Œ≤ + p ‚Ä¢ Œ±`
* `RootPairing.root_add_zsmul_mem_range_iff`: every chain is an interval (aka unbroken).
* `RootPairing.chainBotCoeff_add_chainTopCoeff_le`: every chain has length at most three.

-/

noncomputable section

open FaithfulSMul Function Set Submodule

variable {Œπ R M N : Type*} [Finite Œπ] [CommRing R] [CharZero R] [IsDomain R]
  [AddCommGroup M] [Module R M] [AddCommGroup N] [Module R N]

namespace RootPairing

variable {P : RootPairing Œπ R M N} [P.IsCrystallographic] {i j : Œπ}

/-- Note that it is often more convenient to use `RootPairing.root_add_zsmul_mem_range_iff` than
to invoke this lemma directly. -/
lemma setOf_root_add_zsmul_eq_Icc_of_linearIndependent
    (h : LinearIndependent R ![P.root i, P.root j]) :
    ‚àÉ·µâ (q ‚â§ 0) (p ‚â• 0), {z : ‚Ñ§ | P.root j + z ‚Ä¢ P.root i ‚àà range P.root} = Icc q p := by
  replace h := LinearIndependent.pair_iff.mp <| h.restrict_scalars' ‚Ñ§
  set S : Set ‚Ñ§ := {z | P.root j + z ‚Ä¢ P.root i ‚àà range P.root} with S_def
  have hS‚ÇÄ : 0 ‚àà S := by simp [S]
  have h_fin : S.Finite := by
    suffices Injective (fun z : S ‚Ü¶ z.property.choose) from Finite.of_injective _ this
    intro ‚ü®z, hz‚ü© ‚ü®z', hz'‚ü© hzz
    have : z ‚Ä¢ P.root i = z' ‚Ä¢ P.root i := by
      rwa [‚Üê add_right_inj (P.root j), ‚Üê hz.choose_spec, ‚Üê hz'.choose_spec, P.root.injective.eq_iff]
    have _i : NoZeroSMulDivisors ‚Ñ§ M := have := P.reflexive_left; .int_of_charZero R M
    exact Subtype.ext <| smul_left_injective ‚Ñ§ (P.ne_zero i) this
  have h_ne : S.Nonempty := ‚ü®0, by simp [S_def]‚ü©
  refine ‚ü®sInf S, csInf_le h_fin.bddBelow hS‚ÇÄ, sSup S, le_csSup h_fin.bddAbove hS‚ÇÄ,
    (h_ne.eq_Icc_iff_int h_fin.bddBelow h_fin.bddAbove).mpr fun r ‚ü®k, hk‚ü© s ‚ü®l, hl‚ü© hrs ‚Ü¶ ?_‚ü©
  by_contra! contra
  have hki_notMem : P.root k + P.root i ‚àâ range P.root := by
    replace hk : P.root k + P.root i = P.root j + (r + 1) ‚Ä¢ P.root i := by rw [hk]; module
    replace contra : r + 1 ‚àâ S := hrs.notMem_of_mem_left <| by simp [contra]
    simpa only [hk, S_def, mem_setOf_eq, S] using contra
  have hki_ne : P.root k ‚â† -P.root i := by
    rw [hk]
    contrapose! h
    replace h : r ‚Ä¢ P.root i = - P.root j - P.root i := by rw [‚Üê sub_eq_of_eq_add h.symm]; module
    exact ‚ü®r + 1, 1, by simp [add_smul, h], by omega‚ü©
  have hli_notMem : P.root l - P.root i ‚àâ range P.root := by
    replace hl : P.root l - P.root i = P.root j + (s - 1) ‚Ä¢ P.root i := by rw [hl]; module
    replace contra : s - 1 ‚àâ S := hrs.notMem_of_mem_left <| by simp [lt_sub_right_of_add_lt contra]
    simpa only [hl, S_def, mem_setOf_eq, S] using contra
  have hli_ne : P.root l ‚â† P.root i := by
    rw [hl]
    contrapose! h
    replace h : s ‚Ä¢ P.root i = P.root i - P.root j := by rw [‚Üê sub_eq_of_eq_add h.symm]; module
    exact ‚ü®s - 1, 1, by simp [sub_smul, h], by omega‚ü©
  have h‚ÇÅ : 0 ‚â§ P.pairingIn ‚Ñ§ k i := by
    have := P.root_add_root_mem_of_pairingIn_neg (i := k) (j := i)
    contrapose! this
    exact ‚ü®this, hki_ne, hki_notMem‚ü©
  have h‚ÇÇ : P.pairingIn ‚Ñ§ k i = P.pairingIn ‚Ñ§ j i + r * 2 := by
    apply algebraMap_injective ‚Ñ§ R
    rw [algebraMap_pairingIn, map_add, map_mul, algebraMap_pairingIn, ‚Üê root_coroot'_eq_pairing, hk]
    simp
  have h‚ÇÉ : P.pairingIn ‚Ñ§ l i ‚â§ 0 := by
    have := P.root_sub_root_mem_of_pairingIn_pos (i := l) (j := i)
    contrapose! this
    exact ‚ü®this, fun x ‚Ü¶ hli_ne (congrArg P.root x), hli_notMem‚ü©
  have h‚ÇÑ : P.pairingIn ‚Ñ§ l i = P.pairingIn ‚Ñ§ j i + s * 2 := by
    apply algebraMap_injective ‚Ñ§ R
    rw [algebraMap_pairingIn, map_add, map_mul, algebraMap_pairingIn, ‚Üê root_coroot'_eq_pairing, hl]
    simp
  omega

variable (i j)

open scoped Classical in
/-- If `Œ± = P.root i` and `Œ≤ = P.root j` are linearly independent, this is the value `p ‚â• 0` where
`Œ≤ - q ‚Ä¢ Œ±, ..., Œ≤ - Œ±, Œ≤, Œ≤ + Œ±, ..., Œ≤ + p ‚Ä¢ Œ±` is the `Œ±`-chain through `Œ≤`.

In the absence of linear independence, it takes a junk value. -/
def chainTopCoeff : ‚Ñï :=
  if h : LinearIndependent R ![P.root i, P.root j]
    then (P.setOf_root_add_zsmul_eq_Icc_of_linearIndependent h).choose_spec.2.choose.toNat
    else 0

open scoped Classical in
/-- If `Œ± = P.root i` and `Œ≤ = P.root j` are linearly independent, this is the value `q ‚â• 0` where
`Œ≤ - q ‚Ä¢ Œ±, ..., Œ≤ - Œ±, Œ≤, Œ≤ + Œ±, ..., Œ≤ + p ‚Ä¢ Œ±` is the `Œ±`-chain through `Œ≤`.

In the absence of linear independence, it takes a junk value. -/
def chainBotCoeff : ‚Ñï :=
  if h : LinearIndependent R ![P.root i, P.root j]
    then (-(P.setOf_root_add_zsmul_eq_Icc_of_linearIndependent h).choose).toNat
    else 0

variable {i j}

lemma chainTopCoeff_of_not_linearIndependent (h : ¬¨ LinearIndependent R ![P.root i, P.root j]) :
    P.chainTopCoeff i j = 0 := by
  simp only [chainTopCoeff, h, reduceDIte]

lemma chainBotCoeff_of_not_linearIndependent (h : ¬¨ LinearIndependent R ![P.root i, P.root j]) :
    P.chainBotCoeff i j = 0 := by
  simp only [chainBotCoeff, h, reduceDIte]

variable (h : LinearIndependent R ![P.root i, P.root j])
include h

lemma root_add_nsmul_mem_range_iff_le_chainTopCoeff {n : ‚Ñï} :
    P.root j + n ‚Ä¢ P.root i ‚àà range P.root ‚Üî n ‚â§ P.chainTopCoeff i j := by
  set S : Set ‚Ñ§ := {z | P.root j + z ‚Ä¢ P.root i ‚àà range P.root} with S_def
  suffices (n : ‚Ñ§) ‚àà S ‚Üî n ‚â§ P.chainTopCoeff i j by
    simpa only [S_def, mem_setOf_eq, natCast_zsmul] using this
  have aux : P.chainTopCoeff i j =
      (P.setOf_root_add_zsmul_eq_Icc_of_linearIndependent h).choose_spec.2.choose.toNat := by
    simp [chainTopCoeff, h]
  obtain ‚ü®hp, h‚ÇÇ : S = _‚ü© :=
    (P.setOf_root_add_zsmul_eq_Icc_of_linearIndependent h).choose_spec.2.choose_spec
  rw [aux, h‚ÇÇ, mem_Icc]
  have := (P.setOf_root_add_zsmul_eq_Icc_of_linearIndependent h).choose_spec.1
  omega

lemma root_sub_nsmul_mem_range_iff_le_chainBotCoeff {n : ‚Ñï} :
    P.root j - n ‚Ä¢ P.root i ‚àà range P.root ‚Üî n ‚â§ P.chainBotCoeff i j := by
  set S : Set ‚Ñ§ := {z | P.root j + z ‚Ä¢ P.root i ‚àà range P.root} with S_def
  suffices -(n : ‚Ñ§) ‚àà S ‚Üî n ‚â§ P.chainBotCoeff i j by
    simpa only [S_def, mem_setOf_eq, neg_smul, natCast_zsmul, ‚Üê sub_eq_add_neg] using this
  have aux : P.chainBotCoeff i j =
      (-(P.setOf_root_add_zsmul_eq_Icc_of_linearIndependent h).choose).toNat := by
    simp [chainBotCoeff, h]
  obtain ‚ü®hq, p, hp, h‚ÇÇ : S = _‚ü© :=
    (P.setOf_root_add_zsmul_eq_Icc_of_linearIndependent h).choose_spec
  rw [aux, h‚ÇÇ, mem_Icc]
  omega

lemma Iic_chainTopCoeff_eq :
    Iic (P.chainTopCoeff i j) = {k | P.root j + k ‚Ä¢ P.root i ‚àà range P.root} := by
  ext; simp [‚Üê P.root_add_nsmul_mem_range_iff_le_chainTopCoeff h]

lemma Iic_chainBotCoeff_eq :
    Iic (P.chainBotCoeff i j) = {k | P.root j - k ‚Ä¢ P.root i ‚àà range P.root} := by
  ext; simp [‚Üê P.root_sub_nsmul_mem_range_iff_le_chainBotCoeff h]

omit h in
lemma one_le_chainTopCoeff_of_root_add_mem [P.IsReduced] (h : P.root i + P.root j ‚àà range P.root) :
    1 ‚â§ P.chainTopCoeff i j := by
  have h' := P.linearIndependent_of_add_mem_range_root' h
  rwa [‚Üê root_add_nsmul_mem_range_iff_le_chainTopCoeff h', one_smul, add_comm]

omit h in
lemma one_le_chainBotCoeff_of_root_add_mem [P.IsReduced] (h : P.root i - P.root j ‚àà range P.root) :
    1 ‚â§ P.chainBotCoeff i j := by
  have h' := P.linearIndependent_of_sub_mem_range_root' h
  rwa [‚Üê root_sub_nsmul_mem_range_iff_le_chainBotCoeff h', one_smul, ‚Üê neg_mem_range_root_iff,
    neg_sub]

lemma root_add_zsmul_mem_range_iff {z : ‚Ñ§} :
    P.root j + z ‚Ä¢ P.root i ‚àà range P.root ‚Üî
      z ‚àà Icc (- P.chainBotCoeff i j : ‚Ñ§) (P.chainTopCoeff i j) := by
  rcases z.eq_nat_or_neg with ‚ü®n, rfl | rfl‚ü©
  ¬∑ simp [P.root_add_nsmul_mem_range_iff_le_chainTopCoeff h]
  ¬∑ simp [P.root_sub_nsmul_mem_range_iff_le_chainBotCoeff h, ‚Üê sub_eq_add_neg]

lemma root_sub_zsmul_mem_range_iff {z : ‚Ñ§} :
    P.root j - z ‚Ä¢ P.root i ‚àà range P.root ‚Üî
      z ‚àà Icc (-P.chainTopCoeff i j : ‚Ñ§) (P.chainBotCoeff i j) := by
  rw [sub_eq_add_neg, ‚Üê neg_smul, P.root_add_zsmul_mem_range_iff h, mem_Icc, mem_Icc]
  omega

lemma setOf_root_add_zsmul_mem_eq_Icc :
    {k : ‚Ñ§ | P.root j + k ‚Ä¢ P.root i ‚àà range P.root} =
      Icc (-P.chainBotCoeff i j : ‚Ñ§) (P.chainTopCoeff i j) := by
  ext; simp [‚Üê P.root_add_zsmul_mem_range_iff h]

lemma setOf_root_sub_zsmul_mem_eq_Icc :
    {k : ‚Ñ§ | P.root j - k ‚Ä¢ P.root i ‚àà range P.root} =
      Icc (-P.chainTopCoeff i j : ‚Ñ§) (P.chainBotCoeff i j) := by
  ext; rw [‚Üê root_sub_zsmul_mem_range_iff h, mem_setOf_eq]

lemma chainTopCoeff_eq_sSup :
    P.chainTopCoeff i j = sSup {k | P.root j + k ‚Ä¢ P.root i ‚àà range P.root} := by
  rw [‚Üê Iic_chainTopCoeff_eq h, csSup_Iic]

lemma chainBotCoeff_eq_sSup :
    P.chainBotCoeff i j = sSup {k | P.root j - k ‚Ä¢ P.root i ‚àà range P.root} := by
  rw [‚Üê Iic_chainBotCoeff_eq h, csSup_Iic]

lemma coe_chainTopCoeff_eq_sSup :
    P.chainTopCoeff i j = sSup {k : ‚Ñ§ | P.root j + k ‚Ä¢ P.root i ‚àà range P.root} := by
  rw [setOf_root_add_zsmul_mem_eq_Icc h]
  simp

lemma coe_chainBotCoeff_eq_sSup :
    P.chainBotCoeff i j = sSup {k : ‚Ñ§ | P.root j - k ‚Ä¢ P.root i ‚àà range P.root} := by
  rw [setOf_root_sub_zsmul_mem_eq_Icc h]
  simp

omit h

private lemma chainCoeff_reflectionPerm_left_aux :
    letI := P.indexNeg
    Icc (-P.chainTopCoeff i j : ‚Ñ§) (P.chainBotCoeff i j) =
      Icc (-P.chainBotCoeff (-i) j : ‚Ñ§) (P.chainTopCoeff (-i) j) := by
  letI := P.indexNeg
  by_cases h : LinearIndependent R ![P.root i, P.root j]
  ¬∑ have h' : LinearIndependent R ![P.root (-i), P.root j] := by simpa
    ext z
    rw [‚Üê P.root_add_zsmul_mem_range_iff h', indexNeg_neg, root_reflectionPerm, mem_Icc,
      reflection_apply_self, smul_neg, ‚Üê neg_smul, P.root_add_zsmul_mem_range_iff h, mem_Icc]
    omega
  ¬∑ have h' : ¬¨ LinearIndependent R ![P.root (-i), P.root j] := by simpa
    simp only [chainTopCoeff_of_not_linearIndependent h, chainTopCoeff_of_not_linearIndependent h',
      chainBotCoeff_of_not_linearIndependent h, chainBotCoeff_of_not_linearIndependent h']

@[deprecated (since := "2025-05-28")]
alias chainCoeff_reflection_perm_left_aux := chainCoeff_reflectionPerm_left_aux

private lemma chainCoeff_reflectionPerm_right_aux :
    letI := P.indexNeg
    Icc (-P.chainTopCoeff i j : ‚Ñ§) (P.chainBotCoeff i j) =
      Icc (-P.chainBotCoeff i (-j) : ‚Ñ§) (P.chainTopCoeff i (-j)) := by
  letI := P.indexNeg
  by_cases h : LinearIndependent R ![P.root i, P.root j]
  ¬∑ have h' : LinearIndependent R ![P.root i, P.root (-j)] := by simpa
    ext z
    rw [‚Üê P.root_add_zsmul_mem_range_iff h', indexNeg_neg, root_reflectionPerm, mem_Icc,
      reflection_apply_self, ‚Üê sub_neg_eq_add, ‚Üê neg_sub', neg_mem_range_root_iff,
      P.root_sub_zsmul_mem_range_iff h, mem_Icc]
  ¬∑ have h' : ¬¨ LinearIndependent R ![P.root i, P.root (-j)] := by simpa
    simp only [chainTopCoeff_of_not_linearIndependent h, chainTopCoeff_of_not_linearIndependent h',
      chainBotCoeff_of_not_linearIndependent h, chainBotCoeff_of_not_linearIndependent h']

@[deprecated (since := "2025-05-28")]
alias chainCoeff_reflection_perm_right_aux := chainCoeff_reflectionPerm_right_aux

@[simp]
lemma chainTopCoeff_reflectionPerm_left :
    P.chainTopCoeff (P.reflectionPerm i i) j = P.chainBotCoeff i j := by
  letI := P.indexNeg
  have (z : ‚Ñ§) : z ‚àà Icc (-P.chainTopCoeff i j : ‚Ñ§) (P.chainBotCoeff i j) ‚Üî
      z ‚àà Icc (-P.chainBotCoeff (-i) j : ‚Ñ§) (P.chainTopCoeff (-i) j) := by
    rw [P.chainCoeff_reflectionPerm_left_aux]
  refine le_antisymm ?_ ?_
  ¬∑ simpa using this (P.chainTopCoeff (-i) j)
  ¬∑ simpa using this (P.chainBotCoeff i j)

@[deprecated (since := "2025-05-28")]
alias chainTopCoeff_reflection_perm_left := chainTopCoeff_reflectionPerm_left

@[simp]
lemma chainBotCoeff_reflectionPerm_left :
    P.chainBotCoeff (P.reflectionPerm i i) j = P.chainTopCoeff i j := by
  letI := P.indexNeg
  have (z : ‚Ñ§) : z ‚àà Icc (-P.chainTopCoeff i j : ‚Ñ§) (P.chainBotCoeff i j) ‚Üî
      z ‚àà Icc (-P.chainBotCoeff (-i) j : ‚Ñ§) (P.chainTopCoeff (-i) j) := by
    rw [P.chainCoeff_reflectionPerm_left_aux]
  refine le_antisymm ?_ ?_
  ¬∑ simpa using this (-P.chainBotCoeff (-i) j)
  ¬∑ simpa using this (-P.chainTopCoeff i j)

@[deprecated (since := "2025-05-28")]
alias chainBotCoeff_reflection_perm_left := chainBotCoeff_reflectionPerm_left

@[simp]
lemma chainTopCoeff_reflectionPerm_right :
    P.chainTopCoeff i (P.reflectionPerm j j) = P.chainBotCoeff i j := by
  letI := P.indexNeg
  have (z : ‚Ñ§) : z ‚àà Icc (-P.chainTopCoeff i j : ‚Ñ§) (P.chainBotCoeff i j) ‚Üî
      z ‚àà Icc (-P.chainBotCoeff i (-j) : ‚Ñ§) (P.chainTopCoeff i (-j)) := by
    rw [P.chainCoeff_reflectionPerm_right_aux]
  refine le_antisymm ?_ ?_
  ¬∑ simpa using this (P.chainTopCoeff i (-j))
  ¬∑ simpa using this (P.chainBotCoeff i j)

@[deprecated (since := "2025-05-28")]
alias chainTopCoeff_reflection_perm_right := chainTopCoeff_reflectionPerm_right

@[simp]
lemma chainBotCoeff_reflectionPerm_right :
    P.chainBotCoeff i (P.reflectionPerm j j) = P.chainTopCoeff i j := by
  letI := P.indexNeg
  have (z : ‚Ñ§) : z ‚àà Icc (-P.chainTopCoeff i j : ‚Ñ§) (P.chainBotCoeff i j) ‚Üî
      z ‚àà Icc (-P.chainBotCoeff i (-j) : ‚Ñ§) (P.chainTopCoeff i (-j)) := by
    rw [P.chainCoeff_reflectionPerm_right_aux]
  refine le_antisymm ?_ ?_
  ¬∑ simpa using this (-P.chainBotCoeff i (-j))
  ¬∑ simpa using this (-P.chainTopCoeff i j)

lemma chainBotCoeff_eq_zero_iff :
    P.chainBotCoeff i j = 0 ‚Üî
      ¬¨ LinearIndependent R ![P.root i, P.root j] ‚à® P.root j - P.root i ‚àâ range P.root := by
  by_cases h : LinearIndependent R ![P.root i, P.root j]
  swap; ¬∑ simp [chainBotCoeff_of_not_linearIndependent h, h]
  have : P.chainBotCoeff i j = 0 ‚Üî Iic (P.chainBotCoeff i j) = {0} := by
    simpa [Set.ext_iff, mem_Iic, mem_singleton_iff] using ‚ü®fun h ‚Ü¶ by simp [h], fun h ‚Ü¶ by rw [‚Üê h]‚ü©
  simp only [h, not_true_eq_false, false_or, this, Iic_chainBotCoeff_eq h, Set.ext_iff,
    mem_setOf_eq, mem_singleton_iff]
  refine ‚ü®fun h' ‚Ü¶ by simpa using h' 1, fun h' n ‚Ü¶ ‚ü®fun h'' ‚Ü¶ ?_, fun h'' ‚Ü¶ by simp [h'']‚ü©‚ü©
  replace h' : 1 ‚àâ {k | P.root j - k ‚Ä¢ P.root i ‚àà range P.root} := by simpa using h'
  rw [‚Üê Iic_chainBotCoeff_eq h, mem_Iic, not_le, Nat.lt_one_iff] at h'
  rw [root_sub_nsmul_mem_range_iff_le_chainBotCoeff h] at h''
  omega

lemma chainTopCoeff_eq_zero_iff :
    P.chainTopCoeff i j = 0 ‚Üî
      ¬¨ LinearIndependent R ![P.root i, P.root j] ‚à® P.root j + P.root i ‚àâ range P.root := by
  rw [‚Üê chainBotCoeff_reflectionPerm_left]
  simp [-chainBotCoeff_reflectionPerm_left, chainBotCoeff_eq_zero_iff]

include h

lemma chainBotCoeff_of_add {k : Œπ} (hk : P.root k = P.root j + P.root i) :
    P.chainBotCoeff i k = P.chainBotCoeff i j + 1 := by
  have h' : LinearIndependent R ![P.root i, P.root k] := by simpa [hk, add_comm]
  apply Nat.cast_injective (R := ‚Ñ§)
  rw [Nat.cast_add, Nat.cast_one, coe_chainBotCoeff_eq_sSup h', coe_chainBotCoeff_eq_sSup h]
  have (z : ‚Ñ§) : P.root k - z ‚Ä¢ P.root i = P.root j - (z - 1) ‚Ä¢ P.root i := by rw [hk]; module
  replace this : {z : ‚Ñ§ | P.root k - z ‚Ä¢ P.root i ‚àà range P.root} =
      OrderIso.addRight 1 '' {n | P.root j - n ‚Ä¢ P.root i ‚àà range P.root} := by
    simp [this, sub_eq_add_neg]
  have bdd : BddAbove {z : ‚Ñ§ | P.root j - z ‚Ä¢ P.root i ‚àà range P.root} := by
    rw [setOf_root_sub_zsmul_mem_eq_Icc h]
    exact bddAbove_Icc
  rw [this, ‚Üê OrderIso.map_csSup' _ ‚ü®0, by simp‚ü© bdd, OrderIso.addRight_apply]

lemma chainTopCoeff_of_sub {k : Œπ} (hk : P.root k = P.root j - P.root i) :
    P.chainTopCoeff i k = P.chainTopCoeff i j + 1 := by
  letI := P.indexNeg
  replace hk : P.root k = P.root j + P.root (-i) := by simpa [sub_eq_add_neg] using hk
  simpa using chainBotCoeff_of_add (by simpa) hk

omit h
@[deprecated (since := "2025-05-28")]
alias chainBotCoeff_reflection_perm_right := chainBotCoeff_reflectionPerm_right

variable (i j)

open scoped Classical in
/-- If `Œ± = P.root i` and `Œ≤ = P.root j` are linearly independent, this is the index of the root
`Œ≤ + p ‚Ä¢ Œ±` where `Œ≤ - q ‚Ä¢ Œ±, ..., Œ≤ - Œ±, Œ≤, Œ≤ + Œ±, ..., Œ≤ + p ‚Ä¢ Œ±` is the `Œ±`-chain through `Œ≤`.

In the absence of linear independence, it takes a junk value. -/
def chainTopIdx : Œπ :=
  if h : LinearIndependent R ![P.root i, P.root j]
    then (P.root_add_nsmul_mem_range_iff_le_chainTopCoeff h).mpr
      (le_refl <| P.chainTopCoeff i j) |>.choose
    else j

open scoped Classical in
/-- If `Œ± = P.root i` and `Œ≤ = P.root j` are linearly independent, this is the index of the root
`Œ≤ - q ‚Ä¢ Œ±` where `Œ≤ - q ‚Ä¢ Œ±, ..., Œ≤ - Œ±, Œ≤, Œ≤ + Œ±, ..., Œ≤ + p ‚Ä¢ Œ±` is the `Œ±`-chain through `Œ≤`.

In the absence of linear independence, it takes a junk value. -/
def chainBotIdx : Œπ :=
  if h : LinearIndependent R ![P.root i, P.root j]
    then (P.root_sub_nsmul_mem_range_iff_le_chainBotCoeff h).mpr
      (le_refl <| P.chainBotCoeff i j) |>.choose
    else j

variable {i j}

@[simp]
lemma root_chainTopIdx :
    P.root (P.chainTopIdx i j) = P.root j + P.chainTopCoeff i j ‚Ä¢ P.root i := by
  by_cases h : LinearIndependent R ![P.root i, P.root j]
  ¬∑ simp only [chainTopIdx, reduceDIte, h]
    exact (P.root_add_nsmul_mem_range_iff_le_chainTopCoeff h).mpr
      (le_refl <| P.chainTopCoeff i j) |>.choose_spec
  ¬∑ simp only [chainTopIdx, chainTopCoeff, h, reduceDIte, zero_smul, add_zero]

@[simp]
lemma root_chainBotIdx :
    P.root (P.chainBotIdx i j) = P.root j - P.chainBotCoeff i j ‚Ä¢ P.root i := by
  by_cases h : LinearIndependent R ![P.root i, P.root j]
  ¬∑ simp only [chainBotIdx, reduceDIte, h]
    exact (P.root_sub_nsmul_mem_range_iff_le_chainBotCoeff h).mpr
      (le_refl <| P.chainBotCoeff i j) |>.choose_spec
  ¬∑ simp only [chainBotIdx, chainBotCoeff, h, reduceDIte, zero_smul, sub_zero]

include h

lemma chainBotCoeff_sub_chainTopCoeff :
    P.chainBotCoeff i j - P.chainTopCoeff i j = P.pairingIn ‚Ñ§ j i := by
  suffices ‚àÄ i j, LinearIndependent R ![P.root i, P.root j] ‚Üí
      P.chainBotCoeff i j - P.chainTopCoeff i j ‚â§ P.pairingIn ‚Ñ§ j i by
    refine le_antisymm (this i j h) ?_
    specialize this (P.reflectionPerm i i) j (by simpa)
    simp only [chainBotCoeff_reflectionPerm_left, chainTopCoeff_reflectionPerm_left,
      pairingIn_reflectionPerm_self_right] at this
    omega
  intro i j h
  have h‚ÇÅ : P.reflection i (P.root <| P.chainBotIdx i j) =
      P.root j + (P.chainBotCoeff i j - P.pairingIn ‚Ñ§ j i) ‚Ä¢ P.root i := by
    simp [reflection_apply_root, ‚Üê P.algebraMap_pairingIn ‚Ñ§]
    module
  have h‚ÇÇ : P.reflection i (P.root <| P.chainBotIdx i j) ‚àà range P.root := by
    rw [‚Üê root_reflectionPerm]
    exact mem_range_self _
  rw [h‚ÇÅ, root_add_zsmul_mem_range_iff h, mem_Icc] at h‚ÇÇ
  omega

lemma chainTopCoeff_sub_chainBotCoeff :
    P.chainTopCoeff i j - P.chainBotCoeff i j = - P.pairingIn ‚Ñ§ j i := by
  rw [‚Üê chainBotCoeff_sub_chainTopCoeff h, neg_sub]

omit h

lemma chainCoeff_chainTopIdx_aux :
    P.chainBotCoeff i (P.chainTopIdx i j) = P.chainBotCoeff i j + P.chainTopCoeff i j ‚àß
    P.chainTopCoeff i (P.chainTopIdx i j) = 0 := by
  have aux : LinearIndependent R ![P.root i, P.root j] ‚Üî
      LinearIndependent R ![P.root i, P.root (P.chainTopIdx i j)] := by
    rw [P.root_chainTopIdx, add_comm (P.root j), ‚Üê natCast_zsmul,
      LinearIndependent.pair_add_smul_right_iff]
  by_cases h : LinearIndependent R ![P.root i, P.root j]
  swap; ¬∑ simp [chainTopCoeff_of_not_linearIndependent, chainBotCoeff_of_not_linearIndependent, h]
  have h' : LinearIndependent R ![P.root i, P.root (P.chainTopIdx i j)] := by rwa [‚Üê aux]
  set S‚ÇÅ : Set ‚Ñ§ := {z | P.root j + z ‚Ä¢ P.root i ‚àà range P.root} with S‚ÇÅ_def
  set S‚ÇÇ : Set ‚Ñ§ := {z | P.root (P.chainTopIdx i j) + z ‚Ä¢ P.root i ‚àà range P.root} with S‚ÇÇ_def
  have hS‚ÇÅ‚ÇÇ : S‚ÇÇ = (fun z ‚Ü¶ (-P.chainTopCoeff i j : ‚Ñ§) + z) '' S‚ÇÅ := by
    ext; simp [S‚ÇÅ_def, S‚ÇÇ_def, root_chainTopIdx, add_smul, add_assoc, natCast_zsmul]
  have hS‚ÇÅ : S‚ÇÅ = Icc (- P.chainBotCoeff i j : ‚Ñ§) (P.chainTopCoeff i j) := by
    ext; rw [S‚ÇÅ_def, mem_setOf_eq, root_add_zsmul_mem_range_iff h]
  have hS‚ÇÇ : S‚ÇÇ = Icc (- P.chainBotCoeff i (P.chainTopIdx i j) : ‚Ñ§)
      (P.chainTopCoeff i (P.chainTopIdx i j)) := by
    ext; rw [S‚ÇÇ_def, mem_setOf_eq, root_add_zsmul_mem_range_iff h']
  rw [hS‚ÇÅ, hS‚ÇÇ, image_const_add_Icc, neg_add_cancel, Icc_eq_Icc_iff (by simp), neg_eq_iff_eq_neg,
    neg_add_rev, neg_neg, neg_neg] at hS‚ÇÅ‚ÇÇ
  norm_cast at hS‚ÇÅ‚ÇÇ

@[simp]
lemma chainBotCoeff_chainTopIdx :
    P.chainBotCoeff i (P.chainTopIdx i j) = P.chainBotCoeff i j + P.chainTopCoeff i j :=
  chainCoeff_chainTopIdx_aux.1

@[simp]
lemma chainTopCoeff_chainTopIdx :
    P.chainTopCoeff i (P.chainTopIdx i j) = 0 :=
  chainCoeff_chainTopIdx_aux.2

include h in
lemma chainBotCoeff_add_chainTopCoeff_eq_pairingIn_chainTopIdx :
    P.chainBotCoeff i j + P.chainTopCoeff i j = P.pairingIn ‚Ñ§ (P.chainTopIdx i j) i := by
  replace h : LinearIndependent R ![P.root i, P.root (P.chainTopIdx i j)] := by
    rwa [P.root_chainTopIdx, add_comm (P.root j), ‚Üê natCast_zsmul,
      LinearIndependent.pair_add_smul_right_iff]
  calc (P.chainBotCoeff i j + P.chainTopCoeff i j : ‚Ñ§)
    _ = P.chainBotCoeff i (P.chainTopIdx i j) := by simp
    _ = P.chainBotCoeff i (P.chainTopIdx i j) - P.chainTopCoeff i (P.chainTopIdx i j) := by simp
    _ = P.pairingIn ‚Ñ§ (P.chainTopIdx i j) i := by rw [P.chainBotCoeff_sub_chainTopCoeff h]

lemma chainBotCoeff_add_chainTopCoeff_le_three [P.IsReduced] :
    P.chainBotCoeff i j + P.chainTopCoeff i j ‚â§ 3 := by
  by_cases h : LinearIndependent R ![P.root i, P.root j]
  swap; ¬∑ simp [chainTopCoeff_of_not_linearIndependent, chainBotCoeff_of_not_linearIndependent, h]
  rw [‚Üê Int.ofNat_le, Nat.cast_add, Nat.cast_ofNat,
    chainBotCoeff_add_chainTopCoeff_eq_pairingIn_chainTopIdx h]
  have := P.pairingIn_pairingIn_mem_set_of_isCrystal_of_isRed i (P.chainTopIdx i j)
  aesop

variable (i j) in
lemma chainBotCoeff_add_chainTopCoeff_le_two [P.IsNotG2] :
    P.chainBotCoeff i j + P.chainTopCoeff i j ‚â§ 2 := by
  by_cases h : LinearIndependent R ![P.root i, P.root j]
  swap; ¬∑ simp [chainTopCoeff_of_not_linearIndependent, chainBotCoeff_of_not_linearIndependent, h]
  rw [‚Üê Int.ofNat_le, Nat.cast_add, Nat.cast_ofNat,
    chainBotCoeff_add_chainTopCoeff_eq_pairingIn_chainTopIdx h]
  have := IsNotG2.pairingIn_mem_zero_one_two (P := P) (P.chainTopIdx i j) i
  aesop

/-- For a reduced, crystallographic, irreducible root pairing other than `ùî§‚ÇÇ`, if the sum of two
roots is a root, they cannot make an acute angle.

To see that this lemma fails for `ùî§‚ÇÇ`, let `Œ±` (short) and `Œ≤` (long) be a base. Then the roots
`Œ± + Œ≤` and `2Œ± + Œ≤` make an angle `œÄ / 3` even though `3Œ± + 2Œ≤` is a root. We can even witness as:
```lean
example (P : RootPairing Œπ R M N) [P.EmbeddedG2] :
    P.pairingIn ‚Ñ§ (EmbeddedG2.shortAddLong P) (EmbeddedG2.twoShortAddLong P) = 1 := by
  simp
```
-/
lemma pairingIn_le_zero_of_root_add_mem [P.IsNotG2] (h : P.root i + P.root j ‚àà range P.root) :
    P.pairingIn ‚Ñ§ i j ‚â§ 0 := by
  have aux‚ÇÅ := P.linearIndependent_of_add_mem_range_root' <| add_comm (P.root i) (P.root j) ‚ñ∏ h
  have aux‚ÇÇ := P.chainBotCoeff_add_chainTopCoeff_le_two j i
  have aux‚ÇÉ : 1 ‚â§ P.chainTopCoeff j i := by
    rwa [‚Üê root_add_nsmul_mem_range_iff_le_chainTopCoeff aux‚ÇÅ, one_smul]
  rw [‚Üê P.chainBotCoeff_sub_chainTopCoeff aux‚ÇÅ]
  omega

lemma zero_le_pairingIn_of_root_sub_mem [P.IsNotG2] (h : P.root i - P.root j ‚àà range P.root) :
    0 ‚â§ P.pairingIn ‚Ñ§ i j := by
  replace h : P.root i + P.root (P.reflectionPerm j j) ‚àà range P.root := by
    simpa [-mem_range, ‚Üê sub_eq_add_neg]
  simpa using P.pairingIn_le_zero_of_root_add_mem h

/-- For a reduced, crystallographic, irreducible root pairing other than `ùî§‚ÇÇ`, if the sum of two
roots is a root, the bottom chain coefficient is either one or zero according to whether they are
perpendicular.

To see that this lemma fails for `ùî§‚ÇÇ`, let `Œ±` (short) and `Œ≤` (long) be a base. Then the roots
`Œ±` and `Œ± + Œ≤` provide a counterexample. -/
lemma chainBotCoeff_if_one_zero [P.IsNotG2] (h : P.root i + P.root j ‚àà range P.root) :
    P.chainBotCoeff i j = if P.pairingIn ‚Ñ§ i j = 0 then 1 else 0 := by
  have _i := P.reflexive_left
  have aux‚ÇÅ := P.linearIndependent_of_add_mem_range_root' h
  have aux‚ÇÇ := P.chainBotCoeff_add_chainTopCoeff_le_two i j
  have aux‚ÇÉ : 1 ‚â§ P.chainTopCoeff i j := P.one_le_chainTopCoeff_of_root_add_mem h
  rcases eq_or_ne (P.chainBotCoeff i j) (P.chainTopCoeff i j) with aux‚ÇÑ | aux‚ÇÑ <;>
  simp_rw [P.pairingIn_eq_zero_iff (i := i) (j := j), ‚Üê P.chainBotCoeff_sub_chainTopCoeff aux‚ÇÅ,
    sub_eq_zero, Nat.cast_inj, aux‚ÇÑ, reduceIte] <;>
  omega

lemma chainTopCoeff_if_one_zero [P.IsNotG2] (h : P.root i - P.root j ‚àà range P.root) :
    P.chainTopCoeff i j = if P.pairingIn ‚Ñ§ i j = 0 then 1 else 0 := by
  letI := P.indexNeg
  replace h : P.root i + P.root (-j) ‚àà range P.root := by simpa [‚Üê sub_eq_add_neg] using h
  simpa using P.chainBotCoeff_if_one_zero h

section chainBotCoeff_mul_chainTopCoeff

/-! The proof of lemma 2.6 from [Geck](Geck2017). -/

variable {b : P.Base} {i j k l m : Œπ}
  (hi : i ‚àà b.support) (hj : j ‚àà b.support) (hij : i ‚â† j)
  (h‚ÇÅ : P.root k + P.root i = P.root l)
  (h‚ÇÇ : P.root k - P.root j = P.root m)
  (h‚ÇÉ : P.root k + P.root i - P.root j ‚àà range P.root)

-- TODO Turn this `variable` into a lemma: it is implied by the assumptions above.
variable [P.IsNotG2]

lemma chainBotCoeff_mul_chainTopCoeff.aux_0 (hik_mem : P.root k + P.root i ‚àà range P.root) :
    P.pairingIn ‚Ñ§ k i = 0 ‚à® (P.pairingIn ‚Ñ§ k i < 0 ‚àß P.chainBotCoeff i k = 0) := by
  have _i := P.reflexive_left
  have := pairingIn_le_zero_of_root_add_mem hik_mem
  rw [add_comm] at hik_mem
  rw [P.chainBotCoeff_if_one_zero hik_mem, ite_eq_right_iff, P.pairingIn_eq_zero_iff (i := i)]
  omega

include hi hj hij h‚ÇÅ h‚ÇÇ h‚ÇÉ

/- An auxiliary result en route to `RootPairing.chainBotCoeff_mul_chainTopCoeff`. -/
private lemma chainBotCoeff_mul_chainTopCoeff.aux_1
    (hki : P.pairingIn ‚Ñ§ k i = 0) :
    have _i := P.reflexive_left; letI := P.indexNeg
    P.root i + P.root m ‚àà range P.root ‚Üí P.root j + P.root (-l) ‚àà range P.root ‚Üí
      P.root j + P.root (-k) ‚àà range P.root ‚Üí
    (P.chainBotCoeff i m + 1) * (P.chainBotCoeff j (-k) + 1) =
      (P.chainBotCoeff j (-l) + 1) * (P.chainBotCoeff i k + 1) := by
  intro _ him_mem hjl_mem hjk_mem
  /- Setup some typeclasses and name the 6th root `n`. -/
  letI := P.indexNeg
  have _i : NoZeroSMulDivisors ‚Ñ§ M := NoZeroSMulDivisors.int_of_charZero R M
  obtain ‚ü®n, hn‚ü© := h‚ÇÉ
  /- Establish basic relationships about roots and their sums / differences. -/
  have hnk_ne : n ‚â† k := by rintro rfl; simp [sub_eq_zero, hij, add_sub_assoc] at hn
  have hkj_ne : k ‚â† j ‚àß P.root k ‚â† -P.root j := (IsReduced.linearIndependent_iff _).mp <|
    P.linearIndependent_of_sub_mem_range_root <| h‚ÇÇ ‚ñ∏ mem_range_self m
  have hnk_notMem : P.root n - P.root k ‚àâ range P.root := by
    convert b.sub_notMem_range_root hi hj using 2; rw [hn]; module
  /- Calculate some auxiliary relationships between root pairings. -/
  have aux‚ÇÄ : P.pairingIn ‚Ñ§ j i = - P.pairingIn ‚Ñ§ m i := by
    suffices P.pairing j i = - P.pairing m i from
      algebraMap_injective ‚Ñ§ R <| by simpa only [algebraMap_pairingIn, map_neg]
    replace hki : P.pairing k i = 0 := by rw [‚Üê P.algebraMap_pairingIn ‚Ñ§, hki, map_zero]
    simp only [‚Üê root_coroot_eq_pairing, ‚Üê h‚ÇÇ]
    simp [hki]
  have aux‚ÇÅ : P.pairingIn ‚Ñ§ j i = 0 := by
    refine le_antisymm (b.pairingIn_le_zero_of_ne hij.symm hj hi) ?_
    rw [aux‚ÇÄ, neg_nonneg]
    refine P.pairingIn_le_zero_of_root_add_mem ‚ü®n, ?_‚ü©
    rw [hn, ‚Üê h‚ÇÇ]
    abel
  /- Calculate the pairings between four key root pairs. -/
  have key‚ÇÅ : P.pairingIn ‚Ñ§ i k = 0 := by rwa [pairingIn_eq_zero_iff]
  have key‚ÇÇ : P.pairingIn ‚Ñ§ i m = 0 := P.pairingIn_eq_zero_iff.mp <| by simpa [aux‚ÇÅ] using aux‚ÇÄ
  have key‚ÇÉ : P.pairingIn ‚Ñ§ j k = 2 := by
    suffices 2 ‚â§ P.pairingIn ‚Ñ§ j k by have := IsNotG2.pairingIn_mem_zero_one_two (P := P) j k; aesop
    have hn‚ÇÅ : P.pairingIn ‚Ñ§ n k = 2 + P.pairingIn ‚Ñ§ i k - P.pairingIn ‚Ñ§ j k := by
      apply algebraMap_injective ‚Ñ§ R
      simp only [map_add, map_sub, algebraMap_pairingIn, ‚Üê root_coroot_eq_pairing, hn]
      simp
    have hn‚ÇÇ : P.pairingIn ‚Ñ§ n k ‚â§ 0 := by
      by_contra! contra; exact hnk_notMem <| P.root_sub_root_mem_of_pairingIn_pos contra hnk_ne
    omega
  have key‚ÇÑ : P.pairingIn ‚Ñ§ l j = 1 := by
    have hij : P.pairing i j = 0 := by
      rw [pairing_eq_zero_iff, ‚Üê P.algebraMap_pairingIn ‚Ñ§, aux‚ÇÅ, map_zero]
    have hkj : P.pairing k j = 1 := by
      rw [‚Üê P.algebraMap_pairingIn ‚Ñ§]
      have := P.pairingIn_pairingIn_mem_set_of_isCrystal_of_isRed' j k (by aesop) (by aesop)
      aesop
    apply algebraMap_injective ‚Ñ§ R
    rw [algebraMap_pairingIn, ‚Üê root_coroot_eq_pairing, ‚Üê h‚ÇÅ]
    simp [hkj, hij]
  replace key‚ÇÑ : P.pairingIn ‚Ñ§ j l ‚â† 0 := by rw [ne_eq, P.pairingIn_eq_zero_iff]; omega
  /- Calculate the value of each of the four terms in the goal. -/
  have hik_mem : P.root i + P.root k ‚àà range P.root := ‚ü®l, by rw [‚Üê h‚ÇÅ, add_comm]‚ü©
  simp only [P.chainBotCoeff_if_one_zero, hik_mem, him_mem, hjl_mem, hjk_mem]
  simp [key‚ÇÅ, key‚ÇÇ, key‚ÇÉ, key‚ÇÑ]

/- An auxiliary result en route to `RootPairing.chainBotCoeff_mul_chainTopCoeff`. -/
open RootPositiveForm in
private lemma chainBotCoeff_mul_chainTopCoeff.aux_2
    (hki' : P.pairingIn ‚Ñ§ k i < 0) (hkj' : 0 < P.pairingIn ‚Ñ§ k j) :
    have _i := P.reflexive_left; letI := P.indexNeg
    P.root i + P.root m ‚àà range P.root ‚Üí P.root j + P.root (-l) ‚àà range P.root ‚Üí
      P.root j + P.root (-k) ‚àà range P.root ‚Üí
    ¬¨ (P.chainBotCoeff i m = 1 ‚àß P.chainBotCoeff j (-l) = 0) := by
  intro _ him_mem hjl_mem hjk_mem
  letI := P.indexNeg
  /- Setup some typeclasses. -/
  have _i : NoZeroSMulDivisors ‚Ñ§ M := NoZeroSMulDivisors.int_of_charZero R M
  /- Establish basic relationships about roots and their sums / differences. -/
  have hkj_ne : k ‚â† j ‚àß P.root k ‚â† -P.root j := (IsReduced.linearIndependent_iff _).mp <|
    P.linearIndependent_of_sub_mem_range_root <| h‚ÇÇ ‚ñ∏ mem_range_self m
  have hlj_mem : P.root l - P.root j ‚àà range P.root := by rwa [‚Üê h‚ÇÅ]
  /- It is sufficient to prove that two key pairings vanish. -/
  suffices ¬¨ (P.pairingIn ‚Ñ§ m i = 0 ‚àß P.pairingIn ‚Ñ§ l j ‚â† 0) by
    contrapose! this
    rcases ne_or_eq (P.pairingIn ‚Ñ§ m i) 0 with hmi | hmi
    ¬∑ simpa [hmi, this.1, P.pairingIn_eq_zero_iff (i := i)] using chainBotCoeff_if_one_zero him_mem
    refine ‚ü®hmi, fun hlj ‚Ü¶ ?_‚ü©
    rw [chainBotCoeff_if_one_zero hjl_mem] at this
    simp [P.pairingIn_eq_zero_iff (i := j), hlj] at this
  /- Assume for contradiction that the two pairings do not vanish. -/
  rintro ‚ü®hmi, hlj‚ü©
  /- Use the assumptions to calculate various relationships between root pairings. -/
  have aux‚ÇÄ : P.pairingIn ‚Ñ§ j i = P.pairingIn ‚Ñ§ k i := by
    suffices P.pairing j i = P.pairing k i from
      algebraMap_injective ‚Ñ§ R <| by simpa only [algebraMap_pairingIn]
    replace h‚ÇÇ : P.root k = P.root j + P.root m := (add_eq_of_eq_sub' h‚ÇÇ.symm).symm
    simpa [‚Üê P.root_coroot_eq_pairing k, h‚ÇÇ, ‚Üê P.algebraMap_pairingIn ‚Ñ§]
  obtain ‚ü®aux‚ÇÅ, aux‚ÇÇ‚ü© : P.pairingIn ‚Ñ§ i j = -1 ‚àß P.pairingIn ‚Ñ§ k j = 2 := by
    suffices 0 < - P.pairingIn ‚Ñ§ i j ‚àß - P.pairingIn ‚Ñ§ i j < P.pairingIn ‚Ñ§ k j ‚àß
      P.pairingIn ‚Ñ§ k j ‚â§ 2 by omega
    refine ‚ü®?_, ?_, ?_‚ü©
    ¬∑ rwa [neg_pos, P.pairingIn_lt_zero_iff, aux‚ÇÄ]
    ¬∑ suffices P.pairingIn ‚Ñ§ l j = P.pairingIn ‚Ñ§ i j + P.pairingIn ‚Ñ§ k j by
        have := zero_le_pairingIn_of_root_sub_mem hlj_mem; omega
      suffices P.pairing l j = P.pairing i j + P.pairing k j from
        algebraMap_injective ‚Ñ§ R <| by simpa only [algebraMap_pairingIn, map_add]
      simp [‚Üê P.root_coroot_eq_pairing l, ‚Üê h‚ÇÅ, add_comm]
    ¬∑ have := IsNotG2.pairingIn_mem_zero_one_two (P := P) k j
      aesop
  /- Choose a positive invariant form. -/
  obtain B : RootPositiveForm ‚Ñ§ P := have : Fintype Œπ := Fintype.ofFinite Œπ; P.posRootForm ‚Ñ§
  /- Calculate root length relationships implied by the pairings calculated above. -/
  have ‚ü®aux‚ÇÉ, aux‚ÇÑ‚ü© : B.rootLength i = B.rootLength j ‚àß B.rootLength j < B.rootLength k := by
    have hij_le : B.rootLength i ‚â§ B.rootLength j := B.rootLength_le_of_pairingIn_eq <| Or.inl aux‚ÇÅ
    have hjk_lt : B.rootLength j < B.rootLength k :=
      B.rootLength_lt_of_pairingIn_notMem (by aesop) hkj_ne.2 <| by aesop
    refine ‚ü®?_, hjk_lt‚ü©
    simpa [posForm, rootLength] using (B.toInvariantForm.apply_eq_or_of_apply_ne (i := j) (j := k)
      (by simpa [posForm, rootLength] using hjk_lt.ne) i).resolve_right
      (by simpa [posForm, rootLength] using (lt_of_le_of_lt hij_le hjk_lt).ne)
  /- Use the root length results to calculate a final root pairing. -/
  have aux‚ÇÖ : P.pairingIn ‚Ñ§ k i = -1 := by
    suffices P.pairingIn ‚Ñ§ j i = -1 by omega
    have aux : B.toInvariantForm.form (P.root i) (P.root i) =
        B.toInvariantForm.form (P.root j) (P.root j) := by simpa [posForm, rootLength] using aux‚ÇÉ
    have := P.pairingIn_pairingIn_mem_set_of_length_eq_of_ne aux hij (b.root_ne_neg_of_ne hi hj hij)
    aesop
  /- Use the newly calculated pairing result to obtain further information about root lengths. -/
  have aux‚ÇÜ : B.rootLength k ‚â§ B.rootLength i := B.rootLength_le_of_pairingIn_eq <| Or.inl aux‚ÇÖ
  /- We now have contradictory information about root lengths. -/
  omega

open chainBotCoeff_mul_chainTopCoeff in
/-- This is Lemma 2.6 from [Geck](Geck2017). -/
lemma chainBotCoeff_mul_chainTopCoeff :
    (P.chainBotCoeff i m + 1) * (P.chainTopCoeff j k + 1) =
      (P.chainTopCoeff j l + 1) * (P.chainBotCoeff i k + 1) := by
  /- Setup some typeclasses. -/
  have _i := P.reflexive_left
  letI := P.indexNeg
  suffices (P.chainBotCoeff i m + 1) * (P.chainBotCoeff j (-k) + 1) =
      (P.chainBotCoeff j (-l) + 1) * (P.chainBotCoeff i k + 1) by simpa
  /- Establish basic relationships about roots and their sums / differences. -/
  have him_mem : P.root i + P.root m ‚àà range P.root := by rw [‚Üê h‚ÇÇ]; convert h‚ÇÉ using 1; abel
  have hik_mem : P.root k + P.root i ‚àà range P.root := h‚ÇÅ ‚ñ∏ mem_range_self l
  have hjk_mem : P.root j + P.root (-k) ‚àà range P.root := by
    convert mem_range_self (-m) using 1; simpa [sub_eq_add_neg] using congr(-$h‚ÇÇ)
  have hjl_mem : P.root j + P.root (-l) ‚àà range P.root := by
    rw [h‚ÇÅ, ‚Üê neg_mem_range_root_iff] at h‚ÇÉ; convert h‚ÇÉ using 1; simp [sub_eq_add_neg]
  have h‚ÇÅ' : P.root (-k) - P.root i = P.root (-l) := by
    simp only [root_reflectionPerm, reflection_apply_self, indexNeg_neg]; rw [‚Üê h‚ÇÅ]; abel
  have h‚ÇÇ' : P.root (-k) + P.root j = P.root (-m) := by
    simp only [root_reflectionPerm, reflection_apply_self, indexNeg_neg]; rw [‚Üê h‚ÇÇ]; abel
  have h‚ÇÉ' : P.root (-k) + P.root j - P.root i ‚àà range P.root := by
    simp only [root_reflectionPerm, reflection_apply_self, indexNeg_neg]
    rw [‚Üê neg_mem_range_root_iff]; convert h‚ÇÉ using 1; abel
  /- Proceed to the main argument, following Geck's case splits. It's all just bookkeeping. -/
  rcases aux_0 hik_mem with hki | ‚ü®hki, hik‚ü©
  ¬∑ /- Geck "Case 1" -/
    exact aux_1 hi hj hij h‚ÇÅ h‚ÇÇ h‚ÇÉ hki him_mem hjl_mem hjk_mem
  rw [add_comm] at hik_mem hjk_mem
  rcases aux_0 hjk_mem with hkj | ‚ü®hkj, hjk‚ü©
  ¬∑ /- Geck "Case 2" -/
    simpa only [neg_neg] using (aux_1 hj hi hij.symm h‚ÇÇ' h‚ÇÅ' h‚ÇÉ' hkj hjl_mem
      (by simpa only [neg_neg]) (by simpa only [neg_neg])).symm
  /- Geck "Case 3" -/
  suffices P.chainBotCoeff i m = P.chainBotCoeff j (-l) by rw [hik, hjk, this]
  have aux‚ÇÅ : ¬¨ (P.chainBotCoeff i m = 1 ‚àß P.chainBotCoeff j (-l) = 0) :=
    aux_2 hi hj hij h‚ÇÅ h‚ÇÇ h‚ÇÉ hki (by simpa using hkj) him_mem hjl_mem <| by rwa [add_comm]
  have aux‚ÇÇ : ¬¨(P.chainBotCoeff j (-l) = 1 ‚àß P.chainBotCoeff i m = 0) := by
    simpa using aux_2 hj hi hij.symm h‚ÇÇ' h‚ÇÅ' h‚ÇÉ' hkj (by simpa)
      hjl_mem (by simpa only [neg_neg]) (by simpa only [neg_neg])
  have aux‚ÇÉ : P.chainBotCoeff i m = 0 ‚à® P.chainBotCoeff i m = 1 := by
    have := P.chainBotCoeff_if_one_zero him_mem
    split at this <;> simp [this]
  have aux‚ÇÑ : P.chainBotCoeff j (-l) = 0 ‚à® P.chainBotCoeff j (-l) = 1 := by
    have := P.chainBotCoeff_if_one_zero hjl_mem
    split at this <;> simp only [this, true_or, or_true]
  omega

end chainBotCoeff_mul_chainTopCoeff

end RootPairing
