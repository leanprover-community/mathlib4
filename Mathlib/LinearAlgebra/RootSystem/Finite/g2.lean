/-
Copyright (c) 2025 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import Mathlib.LinearAlgebra.RootSystem.Finite.Lemmas

/-!
# Properties of the `ùî§‚ÇÇ` root system.

Foo bar

## Main results:
 * `RootPairing.EmbeddedG2`: a data-bearing typeclass which distinguishes a pair of roots whose
   pairing is `-3`. This is a sufficient condition for the span of this pair of roots to be a `ùî§‚ÇÇ`
   root system.
 * `RootPairing.EmbeddedG2.span_eq_top`: a finite crystallographic reduced irreducible root pairing
   containing two roots with pairing `-3` is spanned by this pair (thus two-dimensional).

-/

noncomputable section

open Function Set
open MulAction (orbit mem_orbit_iff)
open Submodule (span subset_span)
open FaithfulSMul (algebraMap_injective)

variable {Œπ R M N : Type*} [CommRing R] [AddCommGroup M] [Module R M] [AddCommGroup N] [Module R N]
  (P : RootPairing Œπ R M N)

namespace RootPairing

/-- A data-bearing typeclass which distinguishes a pair of roots whose pairing is `-3`. This is a
sufficient condition for the span of this pair of roots to be a `ùî§‚ÇÇ` root system. -/
class EmbeddedG2 extends P.IsCrystallographic, P.IsReduced where
  long : Œπ
  short : Œπ
  pairingIn_neg_three : P.pairingIn ‚Ñ§ long short = - 3

namespace EmbeddedG2

/-- A pair of roots which pair to `+3` are also sufficient to distinguish an embedded `ùî§‚ÇÇ`. -/
@[simps] def ofPairingInThree [CharZero R] [P.IsCrystallographic] [P.IsReduced] (long short : Œπ)
    (h : P.pairingIn ‚Ñ§ long short = 3) : P.EmbeddedG2 where
  long := P.reflection_perm long long
  short := short
  pairingIn_neg_three := by simp [h]

variable [P.EmbeddedG2]

abbrev shortRoot := P.root (short P)
abbrev longRoot := P.root (long P)

attribute [simp] pairingIn_neg_three

@[simp]
lemma pairing_neg_three : P.pairing (long P) (short P) = - 3 := by
  rw [‚Üê P.algebraMap_pairingIn ‚Ñ§, pairingIn_neg_three]
  simp

variable [Finite Œπ] [CharZero R] [IsDomain R] [NoZeroSMulDivisors R M]

@[simp]
lemma pairingIn_neg_one :
    P.pairingIn ‚Ñ§ (short P) (long P) = - 1 := by
  have := P.pairingIn_pairingIn_mem_set_of_isCrystallographic_of_isReduced (long P) (short P)
  have := pairingIn_neg_three (P := P)
  aesop

@[simp]
lemma pairing_neg_one :
    P.pairing (short P) (long P) = - 1 := by
  rw [‚Üê P.algebraMap_pairingIn ‚Ñ§, pairingIn_neg_one]
  simp

lemma short_add_long_mem_orbit :
    shortRoot P + longRoot P ‚àà orbit P.weylGroup (shortRoot P) := by
  use weylGroup.ofIdx P (long P)
  simp [reflection_apply_root]

lemma two_smul_short_add_long_mem_orbit :
    (2 : R) ‚Ä¢ shortRoot P + longRoot P ‚àà orbit P.weylGroup (shortRoot P) := by
  use weylGroup.ofIdx P (short P) * weylGroup.ofIdx P (long P)
  simp [mul_smul, reflection_apply_root]
  module

omit [Finite Œπ] [CharZero R] [IsDomain R] [NoZeroSMulDivisors R M] in
lemma three_smul_short_add_long_mem_orbit :
    (3 : R) ‚Ä¢ shortRoot P + longRoot P ‚àà orbit P.weylGroup (longRoot P) := by
  use weylGroup.ofIdx P (short P)
  simp [reflection_apply_root]
  module

lemma three_smul_short_add_two_smul_long_mem_orbit :
    (3 : R) ‚Ä¢ shortRoot P + (2 : R) ‚Ä¢ longRoot P ‚àà orbit P.weylGroup (longRoot P) := by
  use weylGroup.ofIdx P (long P) * weylGroup.ofIdx P (short P)
  simp [mul_smul, reflection_apply_root]
  module

variable {P}
variable (B : P.InvariantForm)

lemma long_eq_three_mul_short :
    B.form (longRoot P) (longRoot P) = 3 * B.form (shortRoot P) (shortRoot P) := by
  simpa using B.pairing_mul_eq_pairing_mul_swap (long P) (short P)

-- TODO Replace the lemmas above about orbits and the lemmas below about existentials with
-- a definition for each of these four roots + API about these definitions corresponding to the
-- clauses appearing in the various lemmas.

lemma exists_root_eq_short_add_long_and : ‚àÉ i,
    P.root i = shortRoot P + longRoot P ‚àß
    B.form (P.root i) (P.root i) = B.form (shortRoot P) (shortRoot P) ‚àß
    (‚àÄ j, P.pairing i j = P.pairing (short P) j + P.pairing (long P) j) ‚àß
    (‚àÄ j, P.pairing j i = P.pairing j (short P) + 3 * P.pairing j (long P)) := by
  obtain ‚ü®g, hg‚ü© := mem_orbit_iff.mp <| short_add_long_mem_orbit P
  let i := P.weylGroupToPerm g (short P)
  have hi : P.root i = shortRoot P + longRoot P := by simp [i, ‚Üê hg, Subgroup.smul_def]
  have hi' : B.form (P.root i) (P.root i) = B.form (shortRoot P) (shortRoot P) := by
    rw [‚Üê weylGroup_apply_root, B.apply_weylGroup_smul]
  refine ‚ü®i, hi, hi', fun j ‚Ü¶ mul_right_cancel‚ÇÄ (B.ne_zero j) ?_,
    fun j ‚Ü¶ mul_right_cancel‚ÇÄ (B.ne_zero i) ?_‚ü©
  ¬∑ rw [‚Üê B.two_mul_apply_root_root]; simp [hi, mul_add, add_mul, B.two_mul_apply_root_root]
  ¬∑ calc P.pairing j i * B.form (P.root i) (P.root i)
      _ = 2 * B.form (P.root j) (P.root i) := ?_
      _ = 2 * B.form (P.root j) (shortRoot P) + 2 * B.form (P.root j) (longRoot P) := ?_
      _ = P.pairing j (short P) * B.form (shortRoot P) (shortRoot P) +
            P.pairing j (long P) * B.form (longRoot P) (longRoot P) := ?_
      _ = (P.pairing j (short P) + 3 * P.pairing j (long P)) * B.form (P.root i) (P.root i) := ?_
    ¬∑ rw [B.two_mul_apply_root_root]
    ¬∑ rw [hi, map_add, mul_add]
    ¬∑ rw [B.two_mul_apply_root_root, B.two_mul_apply_root_root]
    ¬∑ rw [long_eq_three_mul_short, hi']; ring

lemma exists_root_eq_two_smul_short_add_long_and : ‚àÉ i,
    P.root i = (2 : R) ‚Ä¢ shortRoot P + longRoot P ‚àß
    B.form (P.root i) (P.root i) = B.form (shortRoot P) (shortRoot P) ‚àß
    (‚àÄ j, P.pairing i j = 2 * P.pairing (short P) j + P.pairing (long P) j) ‚àß
    (‚àÄ j, P.pairing j i = 2 * P.pairing j (short P) + 3 * P.pairing j (long P)) := by
  obtain ‚ü®g, hg‚ü© := mem_orbit_iff.mp <| two_smul_short_add_long_mem_orbit P
  let i := P.weylGroupToPerm g (short P)
  have hi : P.root i = (2 : R) ‚Ä¢ shortRoot P + longRoot P := by simp [i, ‚Üê hg, Subgroup.smul_def]
  have hi' : B.form (P.root i) (P.root i) = B.form (shortRoot P) (shortRoot P) := by
    rw [‚Üê weylGroup_apply_root, B.apply_weylGroup_smul]
  refine ‚ü®i, hi, hi', fun j ‚Ü¶ mul_right_cancel‚ÇÄ (B.ne_zero j) ?_,
    fun j ‚Ü¶ mul_right_cancel‚ÇÄ (B.ne_zero i) ?_‚ü©
  ¬∑ rw [‚Üê B.two_mul_apply_root_root]; simp [hi, mul_add, add_mul, B.two_mul_apply_root_root]; ring
  ¬∑ calc P.pairing j i * B.form (P.root i) (P.root i)
      _ = 2 * B.form (P.root j) (P.root i) := ?_
      _ = 2 * (2 * B.form (P.root j) (shortRoot P)) + 2 * B.form (P.root j) (longRoot P) := ?_
      _ = 2 * P.pairing j (short P) * B.form (shortRoot P) (shortRoot P) +
            P.pairing j (long P) * B.form (longRoot P) (longRoot P) := ?_
      _ = (2 * P.pairing j (short P) + 3 * P.pairing j (long P)) * B.form (P.root i) (P.root i) :=?_
    ¬∑ rw [B.two_mul_apply_root_root]
    ¬∑ rw [hi, map_add, mul_add, map_smul, smul_eq_mul]
    ¬∑ rw [B.two_mul_apply_root_root, B.two_mul_apply_root_root, mul_assoc]
    ¬∑ rw [long_eq_three_mul_short, hi']; ring

lemma exists_root_eq_three_smul_short_add_long_and : ‚àÉ i,
    P.root i = (3 : R) ‚Ä¢ shortRoot P + longRoot P ‚àß
    B.form (P.root i) (P.root i) = B.form (longRoot P) (longRoot P) ‚àß
    (‚àÄ j, P.pairing i j = 3 * P.pairing (short P) j + P.pairing (long P) j) ‚àß
    (‚àÄ j, P.pairing j i = P.pairing j (short P) + P.pairing j (long P)) := by
  obtain ‚ü®g, hg‚ü© := mem_orbit_iff.mp <| three_smul_short_add_long_mem_orbit P
  let i := P.weylGroupToPerm g (long P)
  have hi : P.root i = (3 : R) ‚Ä¢ shortRoot P + longRoot P := by simp [i, ‚Üê hg, Subgroup.smul_def]
  have hi' : B.form (P.root i) (P.root i) = B.form (longRoot P) (longRoot P) := by
    rw [‚Üê weylGroup_apply_root, B.apply_weylGroup_smul]
  refine ‚ü®i, hi, hi', fun j ‚Ü¶ mul_right_cancel‚ÇÄ (B.ne_zero j) ?_,
    fun j ‚Ü¶ mul_right_cancel‚ÇÄ (B.ne_zero i) ?_‚ü©
  ¬∑ rw [‚Üê B.two_mul_apply_root_root]
    simp [hi, mul_add, add_mul, B.two_mul_apply_root_root, mul_left_comm (2 : R) (3 : R)]; ring
  ¬∑ calc P.pairing j i * B.form (P.root i) (P.root i)
      _ = 2 * B.form (P.root j) (P.root i) := ?_
      _ = 3 * (2 * B.form (P.root j) (shortRoot P)) + 2 * B.form (P.root j) (longRoot P) := ?_
      _ = P.pairing j (short P) * B.form (longRoot P) (longRoot P) +
            P.pairing j (long P) * B.form (longRoot P) (longRoot P) := ?_
      _ = (P.pairing j (short P) + P.pairing j (long P)) * B.form (P.root i) (P.root i) := ?_
    ¬∑ rw [B.two_mul_apply_root_root]
    ¬∑ rw [hi, map_add, mul_add, map_smul, smul_eq_mul]; ring
    ¬∑ rw [B.two_mul_apply_root_root, B.two_mul_apply_root_root, long_eq_three_mul_short]; ring
    ¬∑ rw [hi']; ring

lemma exists_root_eq_three_smul_short_add_two_smul_long_and : ‚àÉ i,
    P.root i = (3 : R) ‚Ä¢ shortRoot P + (2 : R) ‚Ä¢ longRoot P ‚àß
    B.form (P.root i) (P.root i) = B.form (longRoot P) (longRoot P) ‚àß
    (‚àÄ j, P.pairing i j = 3 * P.pairing (short P) j + 2 * P.pairing (long P) j) ‚àß
    (‚àÄ j, P.pairing j i = P.pairing j (short P) + 2 * P.pairing j (long P)) := by
  obtain ‚ü®g, hg‚ü© := mem_orbit_iff.mp <| three_smul_short_add_two_smul_long_mem_orbit P
  let i := P.weylGroupToPerm g (long P)
  have hi : P.root i = (3 : R) ‚Ä¢ shortRoot P + (2 : R) ‚Ä¢ longRoot P := by
    simp [i, ‚Üê hg, Subgroup.smul_def]
  have hi' : B.form (P.root i) (P.root i) = B.form (longRoot P) (longRoot P) := by
    rw [‚Üê weylGroup_apply_root, B.apply_weylGroup_smul]
  refine ‚ü®i, hi, hi', fun j ‚Ü¶ mul_right_cancel‚ÇÄ (B.ne_zero j) ?_,
    fun j ‚Ü¶ mul_right_cancel‚ÇÄ (B.ne_zero i) ?_‚ü©
  ¬∑ rw [‚Üê B.two_mul_apply_root_root]
    simp [hi, mul_add, add_mul, B.two_mul_apply_root_root, mul_left_comm (2 : R) (3 : R)]; ring
  ¬∑ calc P.pairing j i * B.form (P.root i) (P.root i)
      _ = 2 * B.form (P.root j) (P.root i) := ?_
      _ = 3 * (2 * B.form (P.root j) (shortRoot P)) + 2 * (2 * B.form (P.root j) (longRoot P)) := ?_
      _ = P.pairing j (short P) * B.form (longRoot P) (longRoot P) +
            2 * P.pairing j (long P) * B.form (longRoot P) (longRoot P) := ?_
      _ = (P.pairing j (short P) + 2 * P.pairing j (long P)) * B.form (P.root i) (P.root i) := ?_
    ¬∑ rw [B.two_mul_apply_root_root]
    ¬∑ rw [hi, map_add, mul_add, map_smul, map_smul, smul_eq_mul, smul_eq_mul]; ring
    ¬∑ rw [B.two_mul_apply_root_root, B.two_mul_apply_root_root, long_eq_three_mul_short]; ring
    ¬∑ rw [hi']; ring

-- TODO Find a way to excise this lemma.
omit [Finite Œπ] [CharZero R] [IsDomain R] [NoZeroSMulDivisors R M] in
private lemma ne_and_ne_neg {k : Œπ} (l : Œπ) (hk : P.root k ‚àâ span R {longRoot P, shortRoot P})
    (hl : P.root l ‚àà span R {longRoot P, shortRoot P}) :
      k ‚â† l ‚àß P.root k ‚â† - P.root l := by
    contrapose! hk
    rcases eq_or_ne k l with rfl | hkl; ¬∑ assumption
    rw [hk hkl]
    exact neg_mem hl

lemma isOrthogonal_short_and_long
    {k : Œπ} (hk : P.root k ‚àâ span R {longRoot P, shortRoot P}) :
    P.IsOrthogonal k (short P) ‚àß P.IsOrthogonal k (long P) := by
  have : Fintype Œπ := Fintype.ofFinite Œπ
  have B := (P.posRootForm ‚Ñ§).toInvariantForm
  suffices P.pairingIn ‚Ñ§ k (short P) = 0 ‚àß P.pairingIn ‚Ñ§ k (long P) = 0 by
    simpa [B.isOrthogonal_iff_pairingIn_eq_zero, ‚Üê P.algebraMap_pairingIn ‚Ñ§]
  obtain ‚ü®i, hi‚ÇÅ, hi‚ÇÇ, hi‚ÇÉ, hi‚ÇÑ‚ü© := exists_root_eq_short_add_long_and B
  obtain ‚ü®j, hj‚ÇÅ, hj‚ÇÇ, hj‚ÇÉ, hj‚ÇÑ‚ü© := exists_root_eq_two_smul_short_add_long_and B
  obtain ‚ü®l, hl‚ÇÅ, hl‚ÇÇ, hl‚ÇÉ, hl‚ÇÑ‚ü© := exists_root_eq_three_smul_short_add_long_and B
  obtain ‚ü®m, hm‚ÇÅ, hm‚ÇÇ, hm‚ÇÉ, hm‚ÇÑ‚ü© := exists_root_eq_three_smul_short_add_two_smul_long_and B
  specialize hi‚ÇÉ k; specialize hi‚ÇÑ k
  specialize hj‚ÇÉ k; specialize hj‚ÇÑ k
  specialize hl‚ÇÉ k; specialize hl‚ÇÑ k
  specialize hm‚ÇÉ k; specialize hm‚ÇÑ k
  have h2 : OfNat.ofNat (2 : ‚Ñï) = (2 : ‚Ñ§) := by rw [‚Üê Int.cast_two]
  have h3 : OfNat.ofNat (3 : ‚Ñï) = (3 : ‚Ñ§) := by rw [‚Üê Int.cast_three]
  simp only [‚Üê P.algebraMap_pairingIn ‚Ñ§, ‚Üê map_add, ‚Üê map_mul, ‚Üê map_ofNat (algebraMap ‚Ñ§ R) 2,
    ‚Üê map_ofNat (algebraMap ‚Ñ§ R) 3, h2, h3,
    (algebraMap_injective ‚Ñ§ R).eq_iff] at hi‚ÇÉ hi‚ÇÑ hj‚ÇÉ hj‚ÇÑ hl‚ÇÉ hl‚ÇÑ hm‚ÇÉ hm‚ÇÑ
  -- TODO Find a way to excise the silly repetition below.
  have hshort : (P.pairingIn ‚Ñ§ k (short P), P.pairingIn ‚Ñ§ (short P) k) ‚àà ({(0, 0), (1, 1), (-1, -1),
      (1, 2), (2, 1), (-1, -2), (-2, -1), (1, 3), (3, 1), (-1, -3), (-3, -1)} : Set (‚Ñ§ √ó ‚Ñ§)) := by
    have := ne_and_ne_neg (short P) hk (subset_span <| mem_insert_of_mem _ rfl)
    exact P.pairingIn_pairingIn_mem_set_of_isCrystallographic_of_isReduced' _ _ this.1 this.2
  have hlong : (P.pairingIn ‚Ñ§ k (long P), P.pairingIn ‚Ñ§ (long P) k) ‚àà ({(0, 0), (1, 1), (-1, -1),
      (1, 2), (2, 1), (-1, -2), (-2, -1), (1, 3), (3, 1), (-1, -3), (-3, -1)} : Set (‚Ñ§ √ó ‚Ñ§)) := by
    have := ne_and_ne_neg (long P) hk (subset_span <| mem_insert _ _)
    exact P.pairingIn_pairingIn_mem_set_of_isCrystallographic_of_isReduced' _ _ this.1 this.2
  have hi‚ÇÖ : (P.pairingIn ‚Ñ§ k i, P.pairingIn ‚Ñ§ i k) ‚àà ({(0, 0), (1, 1), (-1, -1),
      (1, 2), (2, 1), (-1, -2), (-2, -1), (1, 3), (3, 1), (-1, -3), (-3, -1)} : Set (‚Ñ§ √ó ‚Ñ§)) := by
    have := ne_and_ne_neg i hk (hi‚ÇÅ ‚ñ∏ add_mem (subset_span <| mem_insert_of_mem _ rfl)
      (subset_span <| mem_insert _ _))
    exact P.pairingIn_pairingIn_mem_set_of_isCrystallographic_of_isReduced' _ _ this.1 this.2
  have hj‚ÇÖ : (P.pairingIn ‚Ñ§ k j, P.pairingIn ‚Ñ§ j k) ‚àà ({(0, 0), (1, 1), (-1, -1),
      (1, 2), (2, 1), (-1, -2), (-2, -1), (1, 3), (3, 1), (-1, -3), (-3, -1)} : Set (‚Ñ§ √ó ‚Ñ§)) := by
    have := ne_and_ne_neg j hk (hj‚ÇÅ ‚ñ∏ add_mem (Submodule.smul_mem _ _ <| subset_span <|
      mem_insert_of_mem _ rfl) (subset_span <| mem_insert _ _))
    exact P.pairingIn_pairingIn_mem_set_of_isCrystallographic_of_isReduced' _ _ this.1 this.2
  have hl‚ÇÖ : (P.pairingIn ‚Ñ§ k l, P.pairingIn ‚Ñ§ l k) ‚àà ({(0, 0), (1, 1), (-1, -1),
      (1, 2), (2, 1), (-1, -2), (-2, -1), (1, 3), (3, 1), (-1, -3), (-3, -1)} : Set (‚Ñ§ √ó ‚Ñ§)) := by
    have := ne_and_ne_neg l hk (hl‚ÇÅ ‚ñ∏ add_mem (Submodule.smul_mem _ _ <| subset_span <|
      mem_insert_of_mem _ rfl) (subset_span <| mem_insert _ _))
    exact P.pairingIn_pairingIn_mem_set_of_isCrystallographic_of_isReduced' _ _ this.1 this.2
  have hm‚ÇÖ : (P.pairingIn ‚Ñ§ k m, P.pairingIn ‚Ñ§ m k) ‚àà ({(0, 0), (1, 1), (-1, -1),
      (1, 2), (2, 1), (-1, -2), (-2, -1), (1, 3), (3, 1), (-1, -3), (-3, -1)} : Set (‚Ñ§ √ó ‚Ñ§)) := by
    have := ne_and_ne_neg m hk (hm‚ÇÅ ‚ñ∏ add_mem (Submodule.smul_mem _ _ <| subset_span <|
      mem_insert_of_mem _ rfl) (Submodule.smul_mem _ _ <| subset_span <| mem_insert _ _))
    exact P.pairingIn_pairingIn_mem_set_of_isCrystallographic_of_isReduced' _ _ this.1 this.2
  simp at hshort hlong hi‚ÇÖ hj‚ÇÖ hl‚ÇÖ hm‚ÇÖ
  omega

variable (P)

@[simp] lemma span_eq_top [Invertible (2 : R)] [P.IsIrreducible] :
    span R {longRoot P, shortRoot P} = ‚ä§ := by
  have := P.span_root_image_eq_top_of_forall_orthogonal {long P, short P} (by simp)
  rw [show P.root '' {long P, short P} = {longRoot P, shortRoot P} from by aesop] at this
  refine this fun k hk ij hij ‚Ü¶ ?_
  have aux := isOrthogonal_short_and_long hk
  rcases hij with rfl | rfl <;> tauto

end EmbeddedG2

end RootPairing
