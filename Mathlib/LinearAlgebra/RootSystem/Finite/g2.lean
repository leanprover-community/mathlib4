/-
Copyright (c) 2025 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import Mathlib.LinearAlgebra.RootSystem.Finite.Lemmas

/-!
# Properties of the `ùî§‚ÇÇ` Lie algebra.

Foo bar

## Main results:
 * `RootPairing.span_pair_eq_of_coxeterWeight_eq_three`: a finite crystallographic reduced
   irreducible root pairing containing two roots with Coxeter weight three is spanned by this pair.

-/

noncomputable section

open Function Set
open Submodule (span subset_span)
open FaithfulSMul (algebraMap_injective)

variable {Œπ R M N : Type*} [Finite Œπ] [CommRing R] [CharZero R] [IsDomain R]
  [AddCommGroup M] [Module R M] [AddCommGroup N] [Module R N] [NoZeroSMulDivisors R M]
  (P : RootPairing Œπ R M N) [P.IsCrystallographic] [P.IsReduced]

namespace RootPairing

variable {i j k : Œπ}
  -- (hg‚ÇÇ : P.coxeterWeight i j = 3)
  (hg‚ÇÇ : P.pairing i j = -3) -- `i` is long, `j` is short

include hg‚ÇÇ

lemma pairing_swap_eq :
    P.pairing j i = -1 := by
  suffices P.pairingIn ‚Ñ§ j i = -1 by rw [‚Üê P.algebraMap_pairingIn ‚Ñ§, this]; simp
  have : P.pairingIn ‚Ñ§ i j = -3 := by
    rw [‚Üê (FaithfulSMul.algebraMap_injective ‚Ñ§ R).eq_iff, algebraMap_pairingIn]; simpa
  have := P.pairingIn_pairingIn_mem_set_of_isCrystallographic_of_isReduced i j
  aesop

lemma bar‚ÇÅ : ‚àÉ g : P.weylGroup, P.root j + P.root i = g ‚Ä¢ P.root j := by
  use ‚ü®Equiv.reflection P i, P.reflection_mem_weylGroup _‚ü©
  simp [reflection_apply_root, P.pairing_swap_eq hg‚ÇÇ]

lemma bar‚ÇÇ : ‚àÉ g : P.weylGroup, (2 : R) ‚Ä¢ P.root j + P.root i = g ‚Ä¢ P.root j := by
  use ‚ü®Equiv.reflection P j, P.reflection_mem_weylGroup _‚ü© *
    ‚ü®Equiv.reflection P i, P.reflection_mem_weylGroup _‚ü©
  simp [‚Üê Equiv.root_indexEquiv_eq_smul, reflection_apply_root, hg‚ÇÇ, P.pairing_swap_eq hg‚ÇÇ]
  module

omit [Finite Œπ] [CharZero R] [IsDomain R] [NoZeroSMulDivisors R M] [P.IsCrystallographic]
  [P.IsReduced] in
lemma bar‚ÇÉ : ‚àÉ g : P.weylGroup, (3 : R) ‚Ä¢ P.root j + P.root i = g ‚Ä¢ P.root i := by
  use ‚ü®Equiv.reflection P j, P.reflection_mem_weylGroup _‚ü©
  simp [reflection_apply_root, hg‚ÇÇ, add_comm]

lemma bar‚ÇÑ : ‚àÉ g : P.weylGroup, (3 : R) ‚Ä¢ P.root j + (2 : R) ‚Ä¢ P.root i = g ‚Ä¢ P.root i := by
  use ‚ü®Equiv.reflection P i, P.reflection_mem_weylGroup _‚ü© *
    ‚ü®Equiv.reflection P j, P.reflection_mem_weylGroup _‚ü©
  simp [‚Üê Equiv.root_indexEquiv_eq_smul, reflection_apply_root, hg‚ÇÇ, P.pairing_swap_eq hg‚ÇÇ]
  module

lemma length_ratio (B : P.InvariantForm) :
    B.form (P.root i) (P.root i) = 3 * B.form (P.root j) (P.root j) := by
  suffices P.pairing j i = -1 by simpa [hg‚ÇÇ, this] using B.pairing_mul_eq_pairing_mul_swap i j
  replace hg‚ÇÇ : P.pairingIn ‚Ñ§ i j = -3 := by
    apply FaithfulSMul.algebraMap_injective ‚Ñ§ R
    rw [algebraMap_pairingIn, hg‚ÇÇ]
    simp
  suffices P.pairingIn ‚Ñ§ j i = -1 by simp [‚Üê P.algebraMap_pairingIn ‚Ñ§, this]
  have := P.pairingIn_pairingIn_mem_set_of_isCrystallographic_of_isReduced i j
  aesop

lemma foo_short (B : P.InvariantForm)
    (hk : P.root k ‚àâ span R {P.root i, P.root j})
    (short : B.form (P.root k) (P.root k) = B.form (P.root j) (P.root j)) :
    P.IsOrthogonal k i ‚àß P.IsOrthogonal k j := by
  suffices P.pairingIn ‚Ñ§ k i = 0 ‚àß P.pairingIn ‚Ñ§ k j = 0 by
    simp only [‚Üê B.coxeterWeight_zero_iff_isOrthogonal, ‚Üê P.algebraMap_coxeterWeightIn ‚Ñ§]
    simp [coxeterWeightIn, this.1, this.2]

  have aux {l : Œπ} (hl : P.root l ‚àà span R {P.root i, P.root j}) :
      k ‚â† l ‚àß P.root k ‚â† - P.root l := by
    contrapose! hk
    rcases eq_or_ne k l with rfl | hkl; ¬∑ assumption
    rw [hk hkl]
    exact neg_mem hl

  have hj : P.root j ‚àà span R {P.root i, P.root j} := subset_span <| mem_insert_of_mem _ rfl
  have hb : P.pairingIn ‚Ñ§ k j ‚àà Icc (-1) 1 := by
    have := P.pairingIn_pairingIn_mem_set_of_length_eq_of_ne short (aux hj).1 (aux hj).2
    simp at this ‚ä¢
    omega

  obtain ‚ü®g, hg‚ü© := P.bar‚ÇÅ hg‚ÇÇ
  let l := P.weylGroupToPerm g j
  have hl‚ÇÄ : P.root j + P.root i = P.root l := by rwa [weylGroup_apply_root] at hg
  have hl‚ÇÅ : B.form (P.root k) (P.root k) = B.form (P.root l) (P.root l) := by
    rw [‚Üê hl‚ÇÄ, hg, B.apply_weylGroup_smul, short]
  have hl : P.root l ‚àà span R {P.root i, P.root j} := by
    rw [‚Üê hl‚ÇÄ]
    exact add_mem (subset_span <| mem_insert_of_mem _ rfl) (subset_span <| mem_insert _ _)
  have hc : P.pairingIn ‚Ñ§ k l ‚àà Icc (-1) 1 := by
    have := P.pairingIn_pairingIn_mem_set_of_length_eq_of_ne hl‚ÇÅ (aux hl).1 (aux hl).2
    simp at this ‚ä¢
    omega

  obtain ‚ü®g', hg'‚ü© := P.bar‚ÇÇ hg‚ÇÇ
  let m := P.weylGroupToPerm g' j
  have hm‚ÇÄ : (2 : R) ‚Ä¢ P.root j + P.root i = P.root m := by rwa [weylGroup_apply_root] at hg'
  have hm‚ÇÅ : B.form (P.root k) (P.root k) = B.form (P.root m) (P.root m) := by
    rw [‚Üê hm‚ÇÄ, hg', B.apply_weylGroup_smul, short]
  have hm : P.root m ‚àà span R {P.root i, P.root j} := by
    rw [‚Üê hm‚ÇÄ]
    exact add_mem (Submodule.smul_mem _ _ hj) (subset_span <| mem_insert _ _)
  have hd : P.pairingIn ‚Ñ§ k m ‚àà Icc (-1) 1 := by
    have := P.pairingIn_pairingIn_mem_set_of_length_eq_of_ne hm‚ÇÅ (aux hm).1 (aux hm).2
    simp at this ‚ä¢
    omega

  set a := P.pairingIn ‚Ñ§ k i
  set b := P.pairingIn ‚Ñ§ k j
  set c := P.pairingIn ‚Ñ§ k l
  set d := P.pairingIn ‚Ñ§ k m
  simp only [mem_Icc] at hb hc hd

  have habc : c = b + 3 * a := by
    suffices P.pairing k l = P.pairing k j + 3 * P.pairing k i by
      apply FaithfulSMul.algebraMap_injective ‚Ñ§ R
      simp only [a, b, c, map_add, map_mul, algebraMap_pairingIn]
      simpa only [algebraMap_int_eq, eq_intCast, Int.cast_ofNat]
    apply mul_right_cancel‚ÇÄ (B.ne_zero k)
    calc P.pairing k l * B.form (P.root k) (P.root k)
      _ = 2 * B.form (P.root k) (P.root l) := ?_
      _ = 2 * B.form (P.root k) (P.root j) + 2 * B.form (P.root k) (P.root i) := ?_
      _ = P.pairing k j * B.form (P.root k) (P.root k) +
            P.pairing k i * B.form (P.root i) (P.root i) := ?_
      _ = (P.pairing k j + 3 * P.pairing k i) * B.form (P.root k) (P.root k) := ?_
    ¬∑ rw [hl‚ÇÅ, ‚Üê B.two_mul_apply_root_root k l]
    ¬∑ rw [‚Üê hl‚ÇÄ, map_add, mul_add]
    ¬∑ rw [B.two_mul_apply_root_root k j, B.two_mul_apply_root_root k i, short]
    ¬∑ rw [P.length_ratio hg‚ÇÇ B, ‚Üê short]; ring

  have habd : d = 3 * a + 2 * b := by
    suffices P.pairing k m = 3 * P.pairing k i + 2 * P.pairing k j by
      apply FaithfulSMul.algebraMap_injective ‚Ñ§ R
      simp only [a, b, d, map_add, map_mul, algebraMap_pairingIn]
      simpa only [algebraMap_int_eq, eq_intCast, Int.cast_ofNat]
    apply mul_right_cancel‚ÇÄ (B.ne_zero k)
    calc P.pairing k m * B.form (P.root k) (P.root k)
      _ = 2 * B.form (P.root k) (P.root m) := ?_
      _ = 2 * (2 * B.form (P.root k) (P.root j)) + 2 * B.form (P.root k) (P.root i) := ?_
      _ = 2 * P.pairing k j * B.form (P.root k) (P.root k) +
            P.pairing k i * B.form (P.root i) (P.root i) := ?_
      _ = (3 * P.pairing k i + 2 * P.pairing k j) * B.form (P.root k) (P.root k) := ?_
    ¬∑ rw [hm‚ÇÅ, ‚Üê B.two_mul_apply_root_root k m]
    ¬∑ rw [‚Üê hm‚ÇÄ, map_add, mul_add, map_smul, smul_eq_mul]
    ¬∑ rw [B.two_mul_apply_root_root k j, B.two_mul_apply_root_root k i, short, mul_assoc]
    ¬∑ rw [P.length_ratio hg‚ÇÇ B, ‚Üê short]; ring

  omega

lemma foo_long (B : P.InvariantForm)
    (hk : P.root k ‚àâ span R {P.root i, P.root j})
    (long : B.form (P.root k) (P.root k) = B.form (P.root i) (P.root i)) :
    P.IsOrthogonal k i ‚àß P.IsOrthogonal k j := by
  suffices P.pairingIn ‚Ñ§ k i = 0 ‚àß P.pairingIn ‚Ñ§ k j = 0 by
    simp only [‚Üê B.coxeterWeight_zero_iff_isOrthogonal, ‚Üê P.algebraMap_coxeterWeightIn ‚Ñ§]
    simp [coxeterWeightIn, this.1, this.2]

  have aux {l : Œπ} (hl : P.root l ‚àà span R {P.root i, P.root j}) :
      k ‚â† l ‚àß P.root k ‚â† - P.root l := by
    contrapose! hk
    rcases eq_or_ne k l with rfl | hkl; ¬∑ assumption
    rw [hk hkl]
    exact neg_mem hl

  have hi : P.root i ‚àà span R {P.root i, P.root j} := subset_span <| mem_insert _ _
  have ha : P.pairingIn ‚Ñ§ k i ‚àà Icc (-1) 1 := by
    have := P.pairingIn_pairingIn_mem_set_of_length_eq_of_ne long (aux hi).1 (aux hi).2
    simp at this ‚ä¢
    omega

  have hb : P.pairingIn ‚Ñ§ k j ‚àà Icc (-3) 3 ‚àß 3 ‚à£ P.pairingIn ‚Ñ§ k j := by
    suffices P.pairingIn ‚Ñ§ k j = P.pairingIn ‚Ñ§ j k * 3 by
      constructor
      ¬∑ have := P.pairingIn_pairingIn_mem_set_of_isCrystallographic_of_isReduced k j
        aesop
      ¬∑ rw [this]
        exact Int.dvd_mul_left _ 3
    suffices P.pairing k j = P.pairing j k * 3 by
      apply FaithfulSMul.algebraMap_injective ‚Ñ§ R
      simp only [algebraMap_pairingIn, map_mul]
      simpa only [algebraMap_int_eq, eq_intCast, Int.cast_ofNat]
    apply mul_right_cancel‚ÇÄ (B.ne_zero j)
    rw [mul_assoc, ‚Üê P.length_ratio hg‚ÇÇ B, B.pairing_mul_eq_pairing_mul_swap j k, long]

  obtain ‚ü®g, hg‚ü© := P.bar‚ÇÉ hg‚ÇÇ
  let l := P.weylGroupToPerm g i
  have hl‚ÇÄ : (3 : R) ‚Ä¢ P.root j + P.root i = P.root l := by rwa [weylGroup_apply_root] at hg
  have hl‚ÇÅ : B.form (P.root k) (P.root k) = B.form (P.root l) (P.root l) := by
    rw [‚Üê hl‚ÇÄ, hg, B.apply_weylGroup_smul, long]
  have hl : P.root l ‚àà span R {P.root i, P.root j} := by
    rw [‚Üê hl‚ÇÄ]
    exact add_mem (Submodule.smul_mem _ _ <| subset_span <| mem_insert_of_mem _ rfl)
      (subset_span <| mem_insert _ _)
  have he : P.pairingIn ‚Ñ§ k l ‚àà Icc (-1) 1 := by
    have := P.pairingIn_pairingIn_mem_set_of_length_eq_of_ne hl‚ÇÅ (aux hl).1 (aux hl).2
    simp at this ‚ä¢
    omega

  obtain ‚ü®g', hg'‚ü© := P.bar‚ÇÑ hg‚ÇÇ
  let m := P.weylGroupToPerm g' i
  have hm‚ÇÄ : (3 : R) ‚Ä¢ P.root j + (2 : R) ‚Ä¢ P.root i = P.root m := by
    rwa [weylGroup_apply_root] at hg'
  have hm‚ÇÅ : B.form (P.root k) (P.root k) = B.form (P.root m) (P.root m) := by
    rw [‚Üê hm‚ÇÄ, hg', B.apply_weylGroup_smul, long]
  have hm : P.root m ‚àà span R {P.root i, P.root j} := by
    rw [‚Üê hm‚ÇÄ]
    exact add_mem (Submodule.smul_mem _ _ <| subset_span <| mem_insert_of_mem _ rfl)
      (Submodule.smul_mem _ _ <| subset_span <| mem_insert _ _)
  have hf : P.pairingIn ‚Ñ§ k m ‚àà Icc (-1) 1 := by
    have := P.pairingIn_pairingIn_mem_set_of_length_eq_of_ne hm‚ÇÅ (aux hm).1 (aux hm).2
    simp at this ‚ä¢
    omega

  set a := P.pairingIn ‚Ñ§ k i
  set b := P.pairingIn ‚Ñ§ k j
  set e := P.pairingIn ‚Ñ§ k l
  set f := P.pairingIn ‚Ñ§ k m
  simp only [mem_Icc] at ha hb he hf

  have habe : e = a + b := by
    suffices P.pairing k l = P.pairing k i + P.pairing k j by
      apply FaithfulSMul.algebraMap_injective ‚Ñ§ R
      simp only [a, b, e, map_add, map_mul, algebraMap_pairingIn]
      simpa only [algebraMap_int_eq, eq_intCast, Int.cast_ofNat]
    apply mul_right_cancel‚ÇÄ (B.ne_zero k)
    calc P.pairing k l * B.form (P.root k) (P.root k)
      _ = 2 * B.form (P.root k) (P.root l) := ?_
      _ = 3 * (2 * B.form (P.root k) (P.root j)) + 2 * B.form (P.root k) (P.root i) := ?_
      _ = P.pairing k j * B.form (P.root k) (P.root k) +
            P.pairing k i * B.form (P.root i) (P.root i) := ?_
      _ = (P.pairing k i + P.pairing k j) * B.form (P.root k) (P.root k) := ?_
    ¬∑ rw [hl‚ÇÅ, ‚Üê B.two_mul_apply_root_root k l]
    ¬∑ rw [‚Üê hl‚ÇÄ, map_add, map_smul, smul_eq_mul]; ring
    ¬∑ rw [B.two_mul_apply_root_root k j, B.two_mul_apply_root_root k i, long, P.length_ratio hg‚ÇÇ B]
      ring
    ¬∑ rw [long]; ring

  have habf : f = b + 2 * a := by
    suffices P.pairing k m = P.pairing k j + 2 * P.pairing k i by
      apply FaithfulSMul.algebraMap_injective ‚Ñ§ R
      simp only [a, b, f, map_add, map_mul, algebraMap_pairingIn]
      simpa only [algebraMap_int_eq, eq_intCast, Int.cast_ofNat]
    apply mul_right_cancel‚ÇÄ (B.ne_zero k)
    calc P.pairing k m * B.form (P.root k) (P.root k)
      _ = 2 * B.form (P.root k) (P.root m) := ?_
      _ = 3 * (2 * B.form (P.root k) (P.root j)) + 2 * (2 * B.form (P.root k) (P.root i)) := ?_
      _ = P.pairing k j * B.form (P.root k) (P.root k) +
            2 * P.pairing k i * B.form (P.root i) (P.root i) := ?_
      _ = (P.pairing k j + 2 * P.pairing k i) * B.form (P.root k) (P.root k) := ?_
    ¬∑ rw [hm‚ÇÅ, ‚Üê B.two_mul_apply_root_root k m]
    ¬∑ simp only [‚Üê hm‚ÇÄ, map_add, map_smul, smul_eq_mul]; ring
    ¬∑ rw [B.two_mul_apply_root_root k j, B.two_mul_apply_root_root k i, long, P.length_ratio hg‚ÇÇ B]
      ring
    ¬∑ rw [long]; ring

  omega

variable [P.IsIrreducible]

lemma foo (hk : P.root k ‚àâ span R {P.root i, P.root j}) :
    P.IsOrthogonal k i ‚àß P.IsOrthogonal k j := by
  have : Fintype Œπ := Fintype.ofFinite Œπ
  have B : P.InvariantForm := (P.posRootForm ‚Ñ§).toInvariantForm
  have len : B.form (P.root i) (P.root i) = 3 * B.form (P.root j) (P.root j) := by
    simpa [hg‚ÇÇ, P.pairing_swap_eq hg‚ÇÇ] using B.pairing_mul_eq_pairing_mul_swap i j
  have len' : B.form (P.root i) (P.root i) ‚â† B.form (P.root j) (P.root j) := by simp [len]
  rcases B.apply_eq_or_of_apply_ne len' k with long | short
  ¬∑ exact foo_long P hg‚ÇÇ B hk long
  ¬∑ exact foo_short P hg‚ÇÇ B hk short

lemma span_pair_eq_of_coxeterWeight_eq_three [Invertible (2 : R)] :
    span R {P.root i, P.root j} = ‚ä§ := by
  have := P.span_root_image_eq_top_of_forall_orthogonal {i, j} (by simp)
  rw [show P.root '' {i, j} = {P.root i, P.root j} from by aesop] at this
  apply this
  intro k hk ij hij
  have aux := P.foo hg‚ÇÇ hk
  aesop

end RootPairing
